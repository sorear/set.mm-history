$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DirRel $.
  $( Extend class notation with the class of all directed sets. $)
  cdir $a class DirRel $.

  $c tail $.
  $( Extend class notation with the tail function. $)
  ctail $a class tail $.

  $( Define the class of all directed sets/directions. $)
  df-dir $a |- DirRel = { r | ( ( Rel r /\ ( _I |` U. U. r ) C_ r ) /\
     ( ( r o. r ) C_ r /\ ( U. U. r X. U. U. r ) C_ ( `' r o. r ) ) ) } $.

  ${
    $d r x $.
    $( Define the tail function for directed sets. $)
    df-tail $a |- tail = ( r e. DirRel |->
                  ( x e. U. U. r |-> ( r " { x } ) ) ) $.
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y z R $.
    $( Two ways of saying a relation is directed. $)
    codir $p |- ( ( A X. B ) C_ ( `' R o. R ) <-> A. x e. A A. y e. B
      E. z ( x R z /\ y R z ) ) $=
      ( cv cop cxp wcel ccnv ccom wi wal wa wbr wex wss wral visset opelxp brco
      df-br brcnv anbi2i exbii 3bitr3i imbi12i 2albii wrel wb relxp ssrel ax-mp
      r2al 3bitr4i ) AGZBGZHZDEIZJZUSFKZFLZJZMZBNANZUQDJUREJOZUQCGZFPZURVHFPZOZ
      CQZMZBNANUTVCRZVLBESADSVEVMABVAVGVDVLUQURDEBTZUAUQURVCPVIVHURVBPZOZCQVDVL
      CUQURVBFATVOUBUQURVCUCVQVKCVPVJVIVHURFCTVOUDUEUFUGUHUIUTUJVNVFUKDEULABUTV
      CUMUNVLABDEUOUP $.
      $( [22-Nov-2013] $)
  $}

  ${
    $d r A $.  $d r R $.
    isdir.1 $e |- A = U. U. R $.
    $( A condition for a relation to be a direction.  (Contributed by Jeff
       Hankins, 25-Nov-2009.) ) $)
    isdir $p |- ( R e. V -> ( R e. DirRel <-> ( ( Rel R /\ ( _I |` A ) C_ R )
                 /\ ( ( R o. R ) C_ R /\ ( A X. A ) C_ ( `' R o. R ) ) ) ) ) $=
      ( vr cv wrel cid cuni cres wss wa ccom cxp ccnv cdir wceq sseq12d anbi12d
      coeq12d releq unieq unieqd syl6eqr id xpeq12 syl11anc cnveq df-dir elab2g
      reseq2d ) EFZGZHULIZIZJZULKZLZULULMZULKZUOUONZULOZULMZKZLZLBGZHAJZBKZLZBB
      MZBKZAANZBOZBMZKZLZLEBPCULBQZURVIVEVPVQUMVFUQVHULBUAVQUPVGULBVQUOAHVQUOBI
      ZIAVQUNVRULBUBUCDUDZUKVQUEZRSVQUTVKVDVOVQUSVJULBVQULBULBVTVTTVTRVQVAVLVCV
      NVQUOAQZWAVAVLQVSVSUOAUOAUFUGVQVBVMULBULBUHVTTRSSEUIUJ $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  $( A direction is a relation.  (Contributed by Jeff Hankins, 25-Nov-2009.) $)
  reldir $p |- ( R e. DirRel -> Rel R ) $=
    ( cdir wcel wrel cid cuni cres wss wa ccom cxp ccnv eqid isdir ibi simpld )
    ABCZADZEAFFZGAHZQRTIZAAJAHSSKALAJHIZQUAUBISABSMNOPP $.
    $( [22-Nov-2013] $) $( [25-Nov-2009] $)

  $( A direction's domain is equal to its field.  (Contributed by Jeff Hankins,
     25-Nov-2009.) $)
  dirdm $p |- ( R e. DirRel -> dom R = U. U. R ) $=
    ( cdir wcel cdm cuni wss crn cun ssun1 dmrnssfld sstri a1i cres dmresi wrel
    cid wa ccom cxp ccnv eqid isdir ibi simpld simprd dmss syl syl5eqssr eqssd
    ) ABCZADZAEEZUKULFUJUKUKAGZHULUKUMIAJKLUJULPULMZDZUKULNUJUNAFZUOUKFUJAOZUPU
    JUQUPQZAARAFULULSATARFQZUJURUSQULABULUAUBUCUDUEUNAUFUGUHUI $.
    $( [22-Nov-2013] $) $( [25-Nov-2009] $)

  ${
    $d x y z A $.  $d x y z R $.  $d x y z X $.
    dirref.1 $e |- X = dom R $.
    $( A direction is reflexive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirref $p |- ( ( R e. DirRel /\ A e. X ) -> A R A ) $=
      ( cdir wcel wbr cid cres wceq eqid resieq anidms mpbiri cuni cdm wss ccom
      wb wa dirdm syl5eq reseq2d wrel cxp isdir ibi simpld simprd eqsstrd ssbrd
      ccnv syl5 imp ) BEFZACFZAABGZUPAAHCIZGZUOUQUPUSAAJZAKUPUSUTSCAALMNUOURBAA
      UOURHBOOZIZBUOCVAHUOCBPVADBUAUBUCUOBUDZVBBQZUOVCVDTZBBRBQVAVAUEBULBRQTZUO
      VEVFTVABEVAKUFUGUHUIUJUKUMUN $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( A direction is transitive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirtr $p |- ( ( ( R e. DirRel /\ C e. V ) /\ ( A R B /\ B R C ) )
               -> A R C ) $=
      ( vx vy vz cdir wcel wbr wa cvv wi cv wal wss wceq wb breq12 wrel brrelex
      reldir ex anim12d syl w3a ccom cuni cxp ccnv cid cres isdir simprd simpld
      eqid ibi cotr 3adant3 3adant1 anbi12d 3adant2 imbi12d cla43gv syl5 3expia
      sylib com4t mpdd imp31 an32s ) DIJZABDKZBCDKZLZCEJZACDKZVMVPVQVRVMVPAMJZB
      MJZLZVQVRNVMDUAZVPWANDUCWBVNVSVOVTWBVNVSABDUBUDWBVOVTBCDUBUDUEUFWAVQVMVPV
      RVSVTVQVMVPVRNZNVMFOZGOZDKZWEHOZDKZLZWDWGDKZNZHPGPFPZVSVTVQUGWCVMDDUHDQZW
      LVMWMDUIUIZWNUJDUKDUHQZVMWBULWNUMDQLZWMWOLZVMWPWQLWNDIWNUQUNURUOUPFGHDUSV
      HWKWCFGHABCMMEWDARZWEBRZWGCRZUGZWIVPWJVRXAWFVNWHVOWRWSWFVNSWTWDAWEBDTUTWS
      WTWHVOSWRWEBWGCDTVAVBWRWTWJVRSWSWDAWGCDTVCVDVEVFVGVIVJVKVL $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.  $d x y z X $.
    dirge.1 $e |- X = dom R $.
    $( For any two elements of a directed set, there exists a third element
       greater than or equal to both.  (Note that this does not say that the
       two elements have a _least_ upper bound.)  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirge $p |- ( ( R e. DirRel /\ A e. X /\ B e. X )
              -> E. x e. X ( A R x /\ B R x ) ) $=
      ( vy vz cdir wcel cv wbr wa wex cuni eleq2d wral ccom wss simprd wrex cdm
      dirdm syl5eq anbi12d cxp ccnv wrel cres eqid isdir codir sylib wceq breq1
      cid ibi anbi1d exbidv anbi2d rcla42v syl5com sylbid crn reldir cvv visset
      relelrng mp3an1 sylan ex cun ssun2 dmrnssfld sstri syl5sseqr syld adantrd
      sseld idd jcad eximdv df-rex syl6ibr 3impib ) DIJZBEJZCEJZBAKZDLZCWIDLZMZ
      AEUAZWFWGWHMZWLANZWMWFWNBDOOZJZCWPJZMZWOWFWGWQWHWRWFEWPBWFEDUBZWPFDUCUDZP
      WFEWPCXAPUEWFGKZWIDLZHKZWIDLZMZANZHWPQGWPQZWSWOWFWPWPUFDUGDRSZXHWFDDRDSZX
      IWFDUHZUPWPUIDSMZXJXIMZWFXLXMMWPDIWPUJUKUQTTGHAWPWPDULUMXGWOWJXEMZANGHBCW
      PWPXBBUNZXFXNAXOXCWJXEXBBWIDUOURUSXDCUNZXNWLAXPXEWKWJXDCWIDUOUTUSVAVBVCWF
      WOWIEJZWLMZANWMWFWLXRAWFWLXQWLWFWJXQWKWFWJWIDVDZJZXQWFWJXTWFXKWJXTDVEWIVF
      JXKWJXTAVGBWIVFDVHVIVJVKWFXSEWIWFWPXSEXSWTXSVLWPXSWTVMDVNVOXAVPVSVQVRWFWL
      VTWAWBWLAEWCWDVQWE $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d t x y z $.
    $( A totally ordered set is a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tosdir $p |- ( A e. TosetRel -> A e. DirRel ) $=
      ( ccha wcel cdir wrel cid cuni cres wss wa ccom cxp ccnv cps cun eqsstr3d
      jca syl wceq syl5eqssr eqid istoset simplbi psrel posanref inss1 posreltr
      cin a1i simprbi relcoi2 cnvresid cnvss coss1 relcnv relcoi1 ax-mp reseq2d
      relcnvfld coss2 unss sylib sstrd jca32 isdir mpbird ) ABCZADCAEZFAGGZHZAI
      ZJZAAKAIZVIVILZAMZAKZIZJJVGVLVMVQVGANCZVLVGVRVNAVOOZIZAVIVIUAZUBZUCZVRVHV
      KAUDZVRVJAVOUHZAAUEWEAIVRAVOUFUIPZQRVGVRVMWCAUGRVGVNVSVPVGVRVTWBUJVGAVPIZ
      VOVPIZJVSVPIVGWGWHVGAVJAKZVPVGVHWIASVGVRVHWCWDRZAUKRVGVJVOIWIVPIVGVJVJMZV
      OVIULVGVKWKVOIVGVRVKWCWFRZVJAUMRTVJVOAUNRPVGVOVOFVOGGZHZKZVPVOEWOVOSAUOVO
      UPUQVGWNAIWOVPIVGWNVJAVGVIWMFVGVHVIWMSWJAUSRURWLPWNAVOUTRTQAVOVPVAVBVCVDV
      IABWAVEVF $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

