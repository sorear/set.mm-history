$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DirRel $.
  $( Extend class notation with the class of all directed sets. $)
  cdir $a class DirRel $.

  $c tail $.
  $( Extend class notation with the tail function. $)
  ctail $a class tail $.

  ${
    $d d x y z $.
    $( Define the class of all directed sets/directions. $)
    df-dir $a |- DirRel = { d | ( ( Rel d /\ ( _I |` U. U. d ) C_ d )
    /\ ( ( d o. d ) C_ d /\ A. x e. U. U. d A. y e. U. U. d
           E. z e. U. U. d ( x d z /\ y d z ) ) ) } $.
  $}

  ${
    $d t w x y z $.
    $( Define the tail function for directed sets. $)
    df-tail $a |- tail = { <. x , y >. | ( x e. DirRel /\ y = { <. z , w >.
        | ( z e. U. U. x /\ w = { t | z x t } ) } ) } $.
  $}

  ${
    $d d x y z A $.  $d d B $.  $d d x y z D $.
    isdir.1 $e |- A = U. U. D $.
    $( A condition for a relation to be a direction.  (Contributed by Jeff
       Hankins, 25-Nov-2009.) ) $)
    isdir $p |- ( D e. B -> ( D e. DirRel <-> ( ( Rel D /\ ( _I |` A ) C_ D )
  /\ ( ( D o. D ) C_ D /\ A. x e. A A. y e. A E. z e. A ( x D z /\ y D z ) ) )
     ) ) $=
      ( vd cv wrel cid cuni cres wss wa ccom wbr wrex wral cdir wceq releq
      unieq unieqd syl6eqr reseq2d id sseq12d anbi12d coeq1 coeq2 eqtrd breq
      rexeqbidv raleqbidv df-dir elab2g ) HIZJZKURLZLZMZURNZOZURURPZURNZAIZCIZU
      RQZBIZVHURQZOZCVARZBVASZAVASZOZOFJZKDMZFNZOZFFPZFNZVGVHFQZVJVHFQZOZCDRZBD
      SZADSZOZOHFTEURFUAZVDVTVPWIWJUSVQVCVSURFUBWJVBVRURFWJVADKWJVAFLZLDWJUTWKU
      RFUCUDGUEZUFWJUGZUHUIWJVFWBVOWHWJVEWAURFWJVEFURPWAURFURUJURFFUKULWMUHWJVN
      WGAVADWLWJVMWFBVADWLWJVLWECVADWLWJVIWCVKWDVGVHURFUMVJVHURFUMUIUNUOUOUIUIA
      BCHUPUQ $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d x y z D $.
    $( A direction is a relation.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    reldir $p |- ( D e. DirRel -> Rel D ) $=
      ( vx vz vy cdir wcel wrel cid cuni cres wss wa ccom cv wbr wrex wral
      eqid isdir ibi simpld ) AEFZAGZHAIIZJAKZUBUCUELZAAMAKBNCNZAODNUGAOLCUDPDU
      DQBUDQLZUBUFUHLBDCUDEAUDRSTUAUA $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d x y z D $.
    $( A direction's domain is equal to its field.  (Contributed by Jeff
       Hankins, 25-Nov-2009.) $)
    dirdm $p |- ( D e. DirRel -> dom D = U. U. D ) $=
      ( vx vz vy cdir wcel cdm cuni wss crn cun ssun1 dmrnssfld sstri a1i cid
      cres wrel wa ccom cv wbr wrex wral eqid isdir ibi simpld simprd dmresi
      dmss syl5eqssr syl eqssd ) AEFZAGZAHHZUPUQIUOUPUPAJZKUQUPURLAMNOUOPUQQZAI
      ZUQUPIUOARZUTUOVAUTSZAATAIBUACUAZAUBDUAVCAUBSCUQUCDUQUDBUQUDSZUOVBVDSBDCU
      QEAUQUEUFUGUHUIUTUQUSGUPUQUJUSAUKULUMUN $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z D $.  $d x y z X $.
    dirref.1 $e |- X = dom D $.
    $( A direction is reflexive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirref $p |- ( ( D e. DirRel /\ A e. X ) -> A D A ) $=
      ( vx vz vy cdir wcel wa cid cres wbr wceq eqid resieq mpbiri anidms
      adantl wss cuni cdm dirdm syl5eq reseq2d wrel ccom cv wrex wral isdir
      ibi simpld simprd eqsstrd adantr ssbrd mpd ) BHIZACIZJZAAKCLZMZAABMUTVCUS
      UTVCUTUTJVCAANAOCAAPQRSVAVBBAAUSVBBTUTUSVBKBUAUAZLZBUSCVDKUSCBUBVDDBUCUDU
      EUSBUFZVEBTZUSVFVGJZBBUGBTEUHFUHZBMGUHVIBMJFVDUIGVDUJEVDUJJZUSVHVJJEGFVDH
      BVDOUKULUMUNUOUPUQUR $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d x y z H $.
    $( A direction is transitive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirtr $p |- ( ( ( D e. DirRel /\ C e. H ) /\ ( A D B /\ B D C ) )
               -> A D C ) $=
      ( vz vy vx cdir wcel wa wbr cvv wrel reldir brrelex sylan ad2ant2r
      ad2ant2rl wi cv wal ccom wss cuni wrex wral cid cres eqid isdir ibi
      simprd simpld ad2antrr cotr sylib wceq breq1 anbi1d imbi12d 2albidv
      cla4gv ad2antrl mpd breq2 anbi12d imbi1d albidv ad2antll anbi2d ad2antlr
      ex com23 imp mp2and ) DIJZCEJZKZABDLZBCDLZKZKAMJZBMJZACDLZVQVTWCVRWAVQDNZ
      VTWCDOZABDPQRVQWAWDVRVTVQWFWAWDWGBCDPQSVSWBWCWDKZWETVSWHWBWEVSWHWBWETZVSW
      HKZVTBFUAZDLZKZAWKDLZTZFUBZWIWJAGUAZDLZWQWKDLZKZWNTZFUBZGUBZWPWJHUAZWQDLZ
      WSKZXDWKDLZTZFUBGUBZHUBZXCWJDDUCDUDZXJVQXKVRWHVQXKXGWSKFDUEUEZUFGXLUGHXLU
      GZVQWFUHXLUIDUDKZXKXMKZVQXNXOKHGFXLIDXLUJUKULUMUNUOHGFDUPUQWCXJXCTVSWDXIX
      CHAMXDAURZXHXAGFXPXFWTXGWNXPXEWRWSXDAWQDUSUTXDAWKDUSVAVBVCVDVEWDXCWPTVSWC
      XBWPGBMWQBURZXAWOFXQWTWMWNXQWRVTWSWLWQBADVFWQBWKDUSVGVHVIVCVJVEVRWPWITVQW
      HWOWIFCEWKCURZWMWBWNWEXRWLWAVTWKCBDVFVKWKCADVFVAVCVLVEVMVNVOVP $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z D $.  $d x y z X $.
    dirge.1 $e |- X = dom D $.
    $( For any two elements of a directed set, there exists a third element
       greater than or equal to both.  (Note that this does not say that the
       two elements have a _least_ upper bound.)  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirge $p |- ( ( D e. DirRel /\ A e. X /\ B e. X )
              -> E. x e. X ( A D x /\ B D x ) ) $=
      ( vy vz cdir wcel cv wbr wa wrex cuni wral ccom wss wrel cid cres eqid
      isdir ibi simprd wceq breq1 anbi1d rexbidv anbi2d rcla42v syl5com cdm
      dirdm syl5eq eleq2d anbi12d rexeqdv 3imtr4d 3impib ) DIJZBEJZCEJZBAKZDLZC
      VDDLZMZAENZVABDOOZJZCVIJZMZVGAVINZVBVCMVHVAGKZVDDLZHKZVDDLZMZAVINZHVIPGVI
      PZVLVMVADDQDRZVTVADSTVIUADRMZWAVTMZVAWBWCMGHAVIIDVIUBUCUDUEUEVSVMVEVQMZAV
      INGHBCVIVIVNBUFZVRWDAVIWEVOVEVQVNBVDDUGUHUIVPCUFZWDVGAVIWFVQVFVEVPCVDDUGU
      JUIUKULVAVBVJVCVKVAEVIBVAEDUMVIFDUNUOZUPVAEVICWGUPUQVAVGAEVIWGURUSUT $.
      $( [25-Nov-2009] $)
  $}

  ${
    $d t x y z $.
    $( A totally ordered set is a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tosdir $p |- TosetRel C_ DirRel $=
      ( vt vx vy vz ccha cdir cv cps wcel wbr wo cuni wral wa wrel cid cres
      wss ccom wrex psrel ccnv cin posanref inss1 a1i eqsstr3d jca adantr
      posreltr simprl simprr cdm wceq crn psdmrn simpld eleq2d biimpar eqid
      psref syldan ad2ant2r breq2 anbi12d rcla4ev syl12anc expr simplr jaod
      ralimdva imp jca32 cvv wb visset istoset ax-mp isdir 3imtr4i ssriv ) AEFA
      GZHIZBGZCGZWBJZWEWDWBJZKZCWBLLZMZBWIMZNZWBOZPWIQZWBRZNZWBWBSWBRZWDDGZWBJZ
      WEWRWBJZNZDWITZCWIMZBWIMZNNZWBEIZWBFIZWLWPWQXDWCWPWKWCWMWOWBUAWCWNWBWBUBZ
      UCZWBWBUDXIWBRWCWBXHUEUFUGUHUIWCWQWKWBUJUIWCWKXDWCWJXCBWIWCWDWIIZNZWHXBCW
      IXKWEWIIZNWFXBWGXKXLWFXBXKXLWFNNXLWFWEWEWBJZXBXKXLWFUKXKXLWFULWCXLXMXJWFW
      CXLWEWBUMZIZXMWCXOXLWCXNWIWEWCXNWIUNWBUOWIUNWBUPUQZURUSWEWBXNXNUTZVAVBVCX
      AWFXMNDWEWIWRWEUNWSWFWTXMWRWEWDWBVDWRWEWEWBVDVEVFVGVHXKXLWGXBXKXLWGNZNXJW
      DWDWBJZWGXBWCXJXRVIXKXSXRWCXJWDXNIZXSWCXTXJWCXNWIWDXPURUSWDWBXNXQVAVBUIXK
      XLWGULXAXSWGNDWDWIWRWDUNWSXSWTWGWRWDWDWBVDWRWDWEWBVDVEVFVGVHVJVKVKVLVMWBV
      NIZXFWLVOAVPZBCVNWBWIWIUTZVQVRYAXGXEVOYBBCDWIVNWBYCVSVRVTWA $.
      $( [25-Nov-2009] $)
  $}

