$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Directed sets, nets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c DirRel $.
  $( Extend class notation with the class of all directed sets. $)
  cdir $a class DirRel $.

  $c tail $.
  $( Extend class notation with the tail function. $)
  ctail $a class tail $.

  $( Define the class of all directed sets/directions. $)
  df-dir $a |- DirRel = { r | ( ( Rel r /\ ( _I |` U. U. r ) C_ r ) /\
     ( ( r o. r ) C_ r /\ ( U. U. r X. U. U. r ) C_ ( `' r o. r ) ) ) } $.

  ${
    $d r x $.
    $( Define the tail function for directed sets. $)
    df-tail $a |- tail = ( r e. DirRel |->
                  ( x e. U. U. r |-> ( r " { x } ) ) ) $.
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y z R $.
    $( Two ways of saying a relation is directed. $)
    codir $p |- ( ( A X. B ) C_ ( `' R o. R ) <-> A. x e. A A. y e. B
      E. z ( x R z /\ y R z ) ) $=
      ( cv cop cxp wcel ccnv ccom wi wal wa wbr wex wss wral visset opelxp
      brco df-br brcnv anbi2i exbii 3bitr3i imbi12i 2albii wrel wb relxp ssrel
      ax-mp r2al 3bitr4i ) AGZBGZHZDEIZJZUSFKZFLZJZMZBNANZUQDJUREJOZUQCGZFPZURV
      HFPZOZCQZMZBNANUTVCRZVLBESADSVEVMABVAVGVDVLUQURDEBTZUAUQURVCPVIVHURVBPZOZ
      CQVDVLCUQURVBFATVOUBUQURVCUCVQVKCVPVJVIVHURFCTVOUDUEUFUGUHUIUTUJVNVFUKDEU
      LABUTVCUMUNVLABDEUOUP $.
      $( [22-Nov-2013] $)
  $}

  ${
    $d r A $.  $d r R $.
    isdir.1 $e |- A = U. U. R $.
    $( A condition for a relation to be a direction.  (Contributed by Jeff
       Hankins, 25-Nov-2009.) ) $)
    isdir $p |- ( R e. V -> ( R e. DirRel <-> ( ( Rel R /\ ( _I |` A ) C_ R )
                 /\ ( ( R o. R ) C_ R /\ ( A X. A ) C_ ( `' R o. R ) ) ) ) ) $=
      ( vr cv wrel cid cuni cres wss wa ccom cxp ccnv cdir wceq releq unieq
      unieqd syl6eqr reseq2d id sseq12d anbi12d coeq12d xpeq12 syl11anc cnveq
      df-dir elab2g ) EFZGZHULIZIZJZULKZLZULULMZULKZUOUONZULOZULMZKZLZLBGZHAJZB
      KZLZBBMZBKZAANZBOZBMZKZLZLEBPCULBQZURVIVEVPVQUMVFUQVHULBRVQUPVGULBVQUOAHV
      QUOBIZIAVQUNVRULBSTDUAZUBVQUCZUDUEVQUTVKVDVOVQUSVJULBVQULBULBVTVTUFVTUDVQ
      VAVLVCVNVQUOAQZWAVAVLQVSVSUOAUOAUGUHVQVBVMULBULBUIVTUFUDUEUEEUJUK $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  $( A direction is a relation.  (Contributed by Jeff Hankins, 25-Nov-2009.) $)
  reldir $p |- ( R e. DirRel -> Rel R ) $=
    ( cdir wcel wrel cid cuni cres wss wa ccom cxp ccnv eqid isdir ibi simpld
    ) ABCZADZEAFFZGAHZQRTIZAAJAHSSKALAJHIZQUAUBISABSMNOPP $.
    $( [22-Nov-2013] $) $( [25-Nov-2009] $)

  $( A direction's domain is equal to its field.  (Contributed by Jeff Hankins,
     25-Nov-2009.) $)
  dirdm $p |- ( R e. DirRel -> dom R = U. U. R ) $=
    ( cdir wcel cdm cuni wss crn cun ssun1 dmrnssfld sstri a1i cid cres dmresi
    wrel wa ccom cxp ccnv eqid isdir ibi simpld simprd dmss syl syl5eqssr
    eqssd ) ABCZADZAEEZUKULFUJUKUKAGZHULUKUMIAJKLUJULMULNZDZUKULOUJUNAFZUOUKFUJ
    APZUPUJUQUPQZAARAFULULSATARFQZUJURUSQULABULUAUBUCUDUEUNAUFUGUHUI $.
    $( [22-Nov-2013] $) $( [25-Nov-2009] $)

  ${
    $d x y z A $.  $d x y z R $.  $d x y z X $.
    dirref.1 $e |- X = dom R $.
    $( A direction is reflexive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirref $p |- ( ( R e. DirRel /\ A e. X ) -> A R A ) $=
      ( cdir wcel wbr cid cres wceq eqid wb resieq anidms mpbiri cuni cdm
      dirdm syl5eq reseq2d wrel wss wa ccom cxp ccnv isdir ibi simpld simprd
      eqsstrd ssbrd syl5 imp ) BEFZACFZAABGZUPAAHCIZGZUOUQUPUSAAJZAKUPUSUTLCAAM
      NOUOURBAAUOURHBPPZIZBUOCVAHUOCBQVADBRSTUOBUAZVBBUBZUOVCVDUCZBBUDBUBVAVAUE
      BUFBUDUBUCZUOVEVFUCVABEVAKUGUHUIUJUKULUMUN $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( A direction is transitive.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirtr $p |- ( ( ( R e. DirRel /\ C e. V ) /\ ( A R B /\ B R C ) )
               -> A R C ) $=
      ( vx vy vz cdir wcel wbr wa cvv wi wrel reldir brrelex ex anim12d syl cv
      wal w3a ccom wss cuni cxp ccnv cid cres eqid isdir ibi simprd simpld
      cotr sylib wceq wb breq12 3adant3 3adant1 anbi12d 3adant2 imbi12d
      cla43gv syl5 3expia com4t mpdd imp31 an32s ) DIJZABDKZBCDKZLZCEJZACDKZVMV
      PVQVRVMVPAMJZBMJZLZVQVRNVMDOZVPWANDPWBVNVSVOVTWBVNVSABDQRWBVOVTBCDQRSTWAV
      QVMVPVRVSVTVQVMVPVRNZNVMFUAZGUAZDKZWEHUAZDKZLZWDWGDKZNZHUBGUBFUBZVSVTVQUC
      WCVMDDUDDUEZWLVMWMDUFUFZWNUGDUHDUDUEZVMWBUIWNUJDUELZWMWOLZVMWPWQLWNDIWNUK
      ULUMUNUOFGHDUPUQWKWCFGHABCMMEWDAURZWEBURZWGCURZUCZWIVPWJVRXAWFVNWHVOWRWSW
      FVNUSWTWDAWEBDUTVAWSWTWHVOUSWRWEBWGCDUTVBVCWRWTWJVRUSWSWDAWGCDUTVDVEVFVGV
      HVIVJVKVL $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.  $d x y z X $.
    dirge.1 $e |- X = dom R $.
    $( For any two elements of a directed set, there exists a third element
       greater than or equal to both.  (Note that this does not say that the
       two elements have a _least_ upper bound.)  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    dirge $p |- ( ( R e. DirRel /\ A e. X /\ B e. X )
              -> E. x e. X ( A R x /\ B R x ) ) $=
      ( vy vz cdir wcel cv wbr wa wrex wex cuni cdm dirdm syl5eq eleq2d
      anbi12d wral cxp ccnv ccom wss wrel cid cres eqid isdir ibi simprd codir
      sylib wceq breq1 anbi1d exbidv anbi2d rcla42v syl5com sylbid crn reldir
      cvv visset relelrng mp3an1 sylan ex cun ssun2 dmrnssfld sstri syl5sseqr
      sseld syld adantrd idd jcad eximdv df-rex syl6ibr 3impib ) DIJZBEJZCEJZBA
      KZDLZCWIDLZMZAENZWFWGWHMZWLAOZWMWFWNBDPPZJZCWPJZMZWOWFWGWQWHWRWFEWPBWFEDQ
      ZWPFDRSZTWFEWPCXATUAWFGKZWIDLZHKZWIDLZMZAOZHWPUBGWPUBZWSWOWFWPWPUCDUDDUEU
      FZXHWFDDUEDUFZXIWFDUGZUHWPUIDUFMZXJXIMZWFXLXMMWPDIWPUJUKULUMUMGHAWPWPDUNU
      OXGWOWJXEMZAOGHBCWPWPXBBUPZXFXNAXOXCWJXEXBBWIDUQURUSXDCUPZXNWLAXPXEWKWJXD
      CWIDUQUTUSVAVBVCWFWOWIEJZWLMZAOWMWFWLXRAWFWLXQWLWFWJXQWKWFWJWIDVDZJZXQWFW
      JXTWFXKWJXTDVEWIVFJXKWJXTAVGBWIVFDVHVIVJVKWFXSEWIWFWPXSEXSWTXSVLWPXSWTVMD
      VNVOXAVPVQVRVSWFWLVTWAWBWLAEWCWDVRWE $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

  ${
    $d t x y z $.
    $( A totally ordered set is a directed set.  (Contributed by Jeff Hankins,
       25-Nov-2009.) $)
    tosdir $p |- ( A e. TosetRel -> A e. DirRel ) $=
      ( ccha wcel cdir wrel cid cuni cres wss wa ccom cxp ccnv cps cun eqid
      istoset simplbi psrel cin posanref inss1 a1i eqsstr3d jca syl posreltr
      simprbi wceq relcoi2 cnvresid cnvss syl5eqssr coss1 relcnv relcoi1 ax-mp
      relcnvfld reseq2d coss2 unss sylib sstrd jca32 isdir mpbird ) ABCZADCAEZF
      AGGZHZAIZJZAAKAIZVIVILZAMZAKZIZJJVGVLVMVQVGANCZVLVGVRVNAVOOZIZAVIVIPZQZRZ
      VRVHVKASZVRVJAVOTZAAUAWEAIVRAVOUBUCUDZUEUFVGVRVMWCAUGUFVGVNVSVPVGVRVTWBUH
      VGAVPIZVOVPIZJVSVPIVGWGWHVGAVJAKZVPVGVHWIAUIVGVRVHWCWDUFZAUJUFVGVJVOIWIVP
      IVGVJVJMZVOVIUKVGVKWKVOIVGVRVKWCWFUFZVJAULUFUMVJVOAUNUFUDVGVOVOFVOGGZHZKZ
      VPVOEWOVOUIAUOVOUPUQVGWNAIWOVPIVGWNVJAVGVIWMFVGVHVIWMUIWJAURUFUSWLUDWNAVO
      UTUFUMUEAVOVPVAVBVCVDVIABWAVEVF $.
      $( [22-Nov-2013] $) $( [25-Nov-2009] $)
  $}

