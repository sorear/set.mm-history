$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                 (Future - to be reviewed and classified)
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

$( ------------- unsorted stuff ---------------- $)

  $( To run min on:  ompfl3 3jaao 3anor 3ianor n0moeu intprg $)
  $( Renamed:  equnamo -> n0moeu $)
  $( Renamed:  intprd -> intprg, w/ more general antecedents $)
  $( Renamed:  bepart -> elelpwi $)


  ${
    $d x A $.
    $( A response to the notion that the condition ` A =/= (/) ` can be removed
       in ~ r19.2z .  Interestingly enough, ` ph ` does not figure in the
       left-hand side.  ( Contributed by Jeff Hankins, 24-Aug-2009. ) $)
    r19.2zb $p |- ( A =/= (/) <-> ( A. x e. A ph -> E. x e. A ph ) ) $=
      ( c0 wne wral wrex wi r19.2z ex wceq cv wcel noel pm2.21i rgen raleq1
      mpbiri necon3bi wa wex pm3.26 19.22i df-rex n0 3imtr4i ja impbii ) CDEZAB
      CFZABCGZHUIUJUKABCIJUJUKUIUJCDCDKUJABDFABDBLZDMAULNOPABCDQRSULCMZATZBUAUM
      BUAUKUIUNUMBUMAUBUCABCUDBCUEUFUGUH $.
      $( [30-Aug-2009] $) $( [24-Aug-2009] $)
  $}

  $( Two ways to say that ` A ` and ` B ` partition ` C ` (when ` A ` and ` B `
     don't overlap and ` A ` is a part of ` C ` ).  (Contributed by FL,
     17-Nov-2008.) $)
  twpar2 $p |- ( ( A C_ C /\ ( A i^i B ) = (/) )
     -> ( ( A u. B ) = C <-> ( C \ A ) = B ) ) $=
    ( wss cin c0 wceq wa cun cdif wi uncom eqtr eqcom biimpi difeq1 difun2
    incom eqeq1i disj3 bitri expcom eqcoms sylbi syl5com mpan2 syl 3syl mpan
    com12 adantl difss sseq1 unss syl6bi mpi adantr imp eqimss ssundif sylibr
    adantlr eqssd ex impbid ) ACDZABEZFGZHZABIZCGZCAJZBGZVHVKVMKVFVKVHVMBAIZVJG
    ZVKVHVMKZBALVOVKHVNCGZCVNGZVPVNVJCMVQVRVNCNOVRVLVNAJZGZVPCVNAPVTVSBAJZGZVPB
    AQVHVLWAGZVMVTWBHVHBWAGZWCVMKZVHBAEZFGWDVGWFFABRSBATUAWEWABWCWABGVMVLWABMUB
    UCUDVLVSWAMUEUFUGUHUIUJUKVIVMVKVIVMHVJCVIVMVJCDZVFVMWGKVHVMVFWGVMVLCDZVFWGK
    ZCAULVMWHBCDZWIVLBCUMVFWJWGVFWJHWGABCUNOUBUOUPUJUQURVFVMCVJDZVHVFVMHVLBDZWK
    VMWLVFVLBUSUKCABUTVAVBVCVDVE $.
    $( [1-Dec-2010] $) $( [16-Nov-2008] $)

  $( Bring an intersection in and out of a class difference.  (Contributed by
     Jeff Hankins, 15-Jul-2009.) $)
  indif2 $p |- ( A i^i ( B \ C ) ) = ( ( A i^i B ) \ C ) $=
    ( cin cvv cdif inass invdif ineq2i 3eqtr3ri ) ABDZECFZDABLDZDKCFABCFZDABLGK
    CHMNABCHIJ $.
    $( [1-Dec-2010] $) $( [12-Jul-2009] $)

  $( The intersection of two relations is equivalent to a conjunction of those
     relations.  (Contributed by FL, 7-Oct-2008.) $)
  int2rel $p |- ( A ( R i^i S ) B <-> ( A R B /\ A S B ) ) $=
    ( cop cin wcel wa wbr elin df-br anbi12i 3bitr4i ) ABEZCDFZGNCGZNDGZHABOIAB
    CIZABDIZHNCDJABOKRPSQABCKABDKLM $.
    $( [1-Dec-2010] $) $( [6-Oct-2008] $)

  $( The pre-image of the range of a class is the domain of the class.
     (Contributed by Jeff Hankins, 15-Jul-2009.) $)
  cvimarndm $p |- ( `' A " ran A ) = dom A $=
    ( ccnv cdm cima crn imadmrn df-rn imaeq2i dfdm4 3eqtr4i ) ABZKCZDKEKAEZDACK
    FMLKAGHAIJ $.
    $( [1-Dec-2010] $) $( [12-Jul-2009] $)

  ${
    fveq12d.1 $e |- ( ph -> F = G ) $.
    fveq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function value.  (Contributed by FL,
       22-Dec-2008.) $)
    fveq12d $p |- ( ph -> ( F ` A ) = ( G ` B ) ) $=
      ( cfv fveq1d fveq2d eqtrd ) ABDHBEHCEHABDEFIABCEGJK $.
      $( [1-Dec-2010] $) $( [22-Dec-2008] $)
  $}

  ${
    $d A b c $.  $d B b c $.  $d C b c $.
    $( Location of the first element of a Cartesian product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    xp1st $p |- ( A e. ( B X. C ) -> ( 1st ` A ) e. B ) $=
      ( vb vc cxp wcel cv cop wceq wa wex c1st cfv elxp fveq2 visset op1st
      syl6eq eleq1d biimpar adantrr 19.23aivv sylbi ) ABCFGADHZEHZIZJZUEBGZUFCG
      ZKKZELDLAMNZBGZDEABCOUKUMDEUHUIUMUJUHUMUIUHULUEBUHULUGMNUEAUGMPUEUFDQRSTU
      AUBUCUD $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A b c $.  $d B b c $.  $d C b c $.
    $( Location of the second element of a Cartesian product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    xp2nd $p |- ( A e. ( B X. C ) -> ( 2nd ` A ) e. C ) $=
      ( vb vc cxp wcel cv cop wceq wa wex c2nd cfv elxp fveq2 visset op2nd
      syl6eq eleq1d biimpar adantrl 19.23aivv sylbi ) ABCFGADHZEHZIZJZUEBGZUFCG
      ZKKZELDLAMNZCGZDEABCOUKUMDEUHUJUMUIUHUMUJUHULUFCUHULUGMNUFAUGMPUEUFDQEQRS
      TUAUBUCUD $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y z $.  $d B u v w x y z $.  $d ph u v w $.  $d ps u v $.
    $d ch u v $.
    oprabopabf.1 $e |- ( ps -> A. x ps ) $.
    oprabopabf.2 $e |- ( ch -> A. y ch ) $.
    oprabopabf.3 $e |- ( x = ( 1st ` w ) -> ( ph <-> ps ) ) $.
    oprabopabf.4 $e |- ( y = ( 2nd ` w ) -> ( ps <-> ch ) ) $.
    $( Convert between operator and function class abstractions using ` 1st `
       and ` 2nd ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprabopabf $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                          = { <. w , z >. | ( w e. ( A X. B ) /\ ch ) } $=
      ( vu vv cv cop wceq wcel wa wex cab cxp copab2 copab ax-17 c2nd cfv wal
      wi fvex isseti hbal hbim albidv imbi12d mpbii 19.23ai ax-mp hban hbex
      opex opeq1 eqeq2d eleq1 c1st wb fveq2 visset op1st syl6req syl op2nd
      bitr2d anbi12d cla4ev opelxpi sylanr1 wrel relxp 1st2nd mpan opeq1d
      biimpac adantrr xp1st xp2nd jca ad2antrl simprr cvv opeq2 anbi2d cla4egf
      anbi1d 19.22i excom sylib sylan32c 19.23aiv impbii exbii bitri 3bitr4i
      abbii df-oprab df-opab 3eqtr4i ) NPZDPZEPZQZFPZQZRZXJHSZXKISZTZATZTZFUAEU
      AZDUAZNUBXIGPZXMQZRZYCHIUCZSZCTZTZFUAGUAZNUBXSDEFUDYHGFUEYBYJNXTEUAZDUAZF
      UAZYIGUAZFUAYBYJYLYNFYLYNYKYNDYIDGYEYHDYEDUFYGCDYGDUFXKYCUGUHZRZEUACCDUIZ
      UJZEYOYCUGUKZULYPYRECYQEKCEDKUMUNYPBBDUIZUJYRJYPBCYTYQMYPBCDMUOUPUQURUSUT
      UTVAXTYNEYIEGYEYHEYEEUFYGCEYGEUFKUTUTVAXOXLYFSZAYNXRYIXOUUAATZTGXLXJXKVBY
      CXLRZYEXOYHUUBUUCYDXNXIYCXLXMVCVDUUCYGUUACAYCXLYFVEUUCABCUUCXJYCVFUHZRZAB
      VGUUCUUDXLVFUHXJYCXLVFVHXJXKDVIZVJVKLVLUUCYPBCVGUUCYOXLUGUHXKYCXLUGVHXJXK
      UUFEVIVMVKMVLVNVOVOVPXJXKHIVQVRURURYIYLGXIUUDYOQZXMQZRZUUDHSZYOISZTZCYLYI
      YEYGUUICYGYEUUIYGYDUUHXIYGYCUUGXMYFVSYGYCUUGRHIVTYCYFWAWBWCVDWDWEYGUULYEC
      YGUUJUUKYCHIWFYCHIWGWHWIYEYGCWJUUIUULCTZTZXTDUAZEUAZYLUUNXIUUDXKQZXMQZRZU
      UJXQTZBTZTZEUAZUUPYOWKSUUNUVCUJYSUVBUUNEOYOWKOPZYOSEUFUUIUUMEUUIEUFUULCEU
      ULEUFKUTUTYPUUSUUIUVAUUMYPUURUUHXIYPUUQUUGXMXKYOUUDWLWCVDYPUUTUULBCYPXQUU
      KUUJXKYOIVEWMMVOVOWNUSUVBUUOEUUDWKSUVBUUOUJYCVFUKXTUVBDOUUDWKUVDUUDSDUFUU
      SUVADUUSDUFUUTBDUUTDUFJUTUTUUEXOUUSXSUVAUUEXNUURXIUUEXLUUQXMXJUUDXKVCWCVD
      UUEXRUUTABUUEXPUUJXQXJUUDHVEWOLVOVOWNUSWPVLXTEDWQWRWSWTXAXBYBYKFUAZDUAYMY
      AUVEDXTEFWQXBYKDFWQXCYIGFWQXDXEXSDEFNXFYHGFNXGXH $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d ph w $.  $d ps x $.  $d ch y $.
    oprabopab.1 $e |- ( x = ( 1st ` w ) -> ( ph <-> ps ) ) $.
    oprabopab.2 $e |- ( y = ( 2nd ` w ) -> ( ps <-> ch ) ) $.
    $( Convert between operator and function class abstractions using ` 1st `
       and ` 2nd ` .  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    oprabopab $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                          = { <. w , z >. | ( w e. ( A X. B ) /\ ch ) } $=
      ( ax-17 oprabopabf ) ABCDEFGHIBDLCELJKM $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d A u v w x y z $.  $d B u v w x y z $.  $d C u v w z $.
    $d D u v w x y z $.  $d F u v $.  $d G u v z $.  $d H u v w x y z $.
    oprabco.1 $e |- ( ( x e. A /\ y e. B ) -> C e. D ) $.
    oprabco.2 $e |- F = { <. <. x , y >. , z >. |
                                ( ( x e. A /\ y e. B ) /\ z = C ) } $.
    oprabco.3 $e |- G = { <. <. x , y >. , w >. |
                                ( ( x e. A /\ y e. B ) /\ w = ( H ` C ) ) } $.
    $( Composition of a function with an operator abstraction.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    oprabco $p |- ( H Fn D -> G = ( H o. F ) ) $=
      ( vu vv wfn ccom wceq cxp cv cfv wral wa eqidd wcel c2nd c1st csb rgen2
      ax-17 fvex hbcsb1 hbel hbral csbeq1a eleq1d ralbidv rcla4 sylan9 mpi
      xp1st xp2nd sylanc fveq2 csbeq1d cvv csbeq2dv mpan2 eqtrd copab2 copab
      hbeq eqeq2d oprabopabf eqtri fvopab4g mpdan adantl fveq2d csbfv2g ax-mp
      csbeq2i syl6eqr wfun cdm fvco fnfun adantr wf foprab ffn a1i fdmi eleq2i
      biimpri syl3anc csbex 3eqtr4rd r19.21aiva jca wb eqfnfv fnoprab2 crn wss
      frn fnco mp3an23 sylancr mpbird ) KHQZJKIRZSZEFTZXOSZOUAZJUBZXQXMUBZSZOXO
      UCZUDZXLXPYAXLXOUEXLXTOXOXLXQXOUFZUDZXQIUBZKUBZBXQUGUBZAXQUHUBZGKUBZUIZUI
      ZXSXRYDYFBYGAYHGUIZUIZKUBZYKYDYEYMKYCYEYMSZXLYCYMHUFZYOYHEUFZYGFUFZYPYCYQ
      YRUDGHUFZBFUCZAEUCZYPYSABEFLUJYQUUAYLHUFZBFUCZYRYPYTUUCAYHEUUBABFBUAZFUFZ
      AUKAPPYLHAPYHGXQUHULZPUAZYHUFAUKUMUUGHUFZAUKUNUOAUAZYHSZYSUUBBFUUJGYLHAYH
      GUPUQURUSUUBYPBYGFBPPYMHBPYGYLXQUGULZUUGYGUFBUKUMUUHBUKUNUUDYGSYLYMHBYGYL
      UPUQUSUTVAXQEFVBXQEFVCVDPCXQBUUGUGUBZAUUGUHUBZGUIZUIZYMXOHIUUGXQSZUUOBYGU
      UNUIZYMUUPBUULYGUUNUUGXQUGVEZVFUUPYGVGUFZUUQYMSUUKUUPBYGUUNYLVGUUPAUUMYHG
      UUGXQUHVEZVFVHVIVJIUUIEUFUUEUDZCUAZGSZUDABCVKUUGXOUFZUVBUUOSZUDPCVLMUVCUV
      BUUNSUVEABCPEFAOOUVBUUNXQUVBUFZAUKAOUUMGUUGUHULZXQUUMUFAUKZUMVMBOOUVBUUOU
      VFBUKBOUULUUNUUGUGULZXQUULUFBUKZUMVMUUIUUMSZGUUNUVBAUUMGUPVNUUDUULSZUUNUU
      OUVBBUULUUNUPVNVOVPVQVRVSVTYKBYGYLKUBZUIZYNUUSYKUVNSUUKBYGYJUVMVGYHVGUFYJ
      UVMSUUFAYHGVGKWAWBWCWBUUSUVNYNSUUKBYGYLVGKWAWBVPWDKWEZIWEZXQIWFZUFZXSYFSY
      DXQKIWGXLUVOYCHKWHWIUVPYDIXOQZUVPXOHIWJZUVSABCEFGHIMLWKZXOHIWLWBZXOIWHWBW
      MYCUVRXLUVRYCUVQXOXQXOHIUWAWNWOWPVSWQYCXRYKSZXLYCYKVGUFUWCBYGYJUUKAYHYIUU
      FGKULZWRWRPDXQBUULAUUMYIUIZUIZYKXOVGJUUPUWFBYGUWEUIZYKUUPBUULYGUWEUURVFUU
      PUUSUWGYKSUUKUUPBYGUWEYJVGUUPAUUMYHYIUUTVFVHVIVJJUVADUAZYISZUDABDVKUVDUWH
      UWFSZUDPDVLNUWIUWHUWESUWJABDPEFAOOUWHUWEXQUWHUFZAUKAOUUMYIUVGUVHUMVMBOOUW
      HUWFUWKBUKBOUULUWEUVIUVJUMVMUVKYIUWEUWHAUUMYIUPVNUVLUWEUWFUWHBUULUWEUPVNV
      OVPVQVIVSWSWTXAJXOQXMXOQZXNYBXBXLOXOXOJXMXCABDEFYIJUWDNXDXLUVSIXEHXFZUWLU
      WBUVTUWMUWAXOHIXGWBHXOKIXHXIXJXK $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z w $.  $d B x y z w $.  $d C z w $.  $d D z w $.  $d R x y z w $.
    $d S x y z w $.  $d M x y z w $.  $d F w $.  $d G z $.
    oprab2co.1 $e |- ( ( x e. A /\ y e. B ) -> C e. R ) $.
    oprab2co.2 $e |- ( ( x e. A /\ y e. B ) -> D e. S ) $.
    oprab2co.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
                                                  /\ z = <. C , D >. ) } $.
    oprab2co.4 $e |- G = { <. <. x , y >. , w >. | ( ( x e. A /\ y e. B )
                                                  /\ w = ( C M D ) ) } $.
    $( Composition of operator abstractions.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    oprab2co $p |- ( M Fn ( R X. S ) -> G = ( M o. F ) ) $=
      ( cop cxp wcel cv wa opelxpi sylanc co wceq copab2 cfv df-opr eqeq2i
      anbi2i oprabbii eqtri oprabco ) ABCDEFGHRZIJSZKLMGITHJTUOUPTAUAETBUAFTUBZ
      GHIJUCNOUDPLUQDUAZGHMUEZUFZUBZABDUGUQURUOMUHZUFZUBZABDUGQVAVDABDUTVCUQUSV
      BURGHMUIUJUKULUMUN $.
      $( [1-Dec-2010] $) $( [2-Sep-2009] $)
  $}

  ${
    $( Membership in the preimage of a set under a function.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    elpreima $p |- ( F Fn A -> ( B e. ( `' F " C )
                                <-> ( B e. A /\ ( F ` B ) e. C ) ) ) $=
      ( wfn ccnv cima wcel cfv wa cdm fndm eleq2d cnvimass sseli syl5bi wfun
      fvimacnvi fnfun sylan ex jcad wb fvimacnv funfni biimpd expimpd impbid )
      DAEZBDFCGZHZBAHZBDICHZJUIUKULUMUIBDKZHULUKUIUNABADLMUJUNBDCNOPUIUKUMDQUKU
      MUIBCDRADSTUAUBUIULUMUKUIULJUMUKUMUKUCABDBCDUDUEUFUGUH $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  $( The empty set is finite.  (Contributed by FL, 2-Sep-2008.) $)
  emfin $p |- (/) e. Fin $=
    ( c0 com wcel wss cfn peano1 ssid ssnnfi mp2an ) ABCAADAECFAGAAHI $.
    $( [1-Dec-2010] $) $( [14-Jul-2008] $)

  $( If a union is finite, the operands are finite.  (Contributed by FL,
     3-Aug-2009.) $)
  unfin $p |- ( ( A u. B ) e. Fin -> ( A e. Fin /\ B e. Fin ) ) $=
    ( cun cfn wcel wss ssun1 ssfi mpan2 ssun2 jca ) ABCZDEZADEZBDEZMALFNABGLAHI
    MBLFOBAJLBHIK $.
    $( [1-Dec-2010] $) $( [10-Jul-2009] $)

  $( If ` A ` is finite, ` ( A \ B ) ` is finite.  (Contributed by FL,
     3-Aug-2009.) $)
  finfin $p |- ( A e. Fin -> ( A \ B ) e. Fin ) $=
    ( cfn wcel cdif wss difss ssfi mpan2 ) ACDABEZAFJCDABGAJHI $.
    $( [1-Dec-2010] $) $( [10-Jul-2009] $)

  $( An infinite set ` A ` minus a finite set is infinite.  (Contributed by FL,
     3-Aug-2009.) $)
  islfin $p |- ( ( -. A e. Fin /\ B e. Fin ) -> -. ( A \ B ) e. Fin ) $=
    ( cfn wcel wn cdif wa cun unfi undif1 eleq1i unfin pm3.26d sylbi syl
    expcom con3d impcom ) BCDZACDZEABFZCDZESUBTUBSTUBSGUABHZCDZTUABIUDABHZCDZTU
    CUECABJKUFTSABLMNOPQR $.
    $( [1-Dec-2010] $) $( [10-Jul-2009] $)

  ${
    $( If ` A ` is equinumerous to a natural number, then that number is its
       cardinal.  Contributed by Jeff Madsen 2-Sep-2009. $)
    cardennn $p |- ( ( A e. C /\ A ~~ B /\ B e. om ) -> ( card ` A ) = B ) $=
      ( wcel cen wbr com w3a ccrd cfv wceq wi wa carden biimprd ex com23 3imp
      cardnn 3ad2ant3 eqtrd ) ACDZABEFZBGDZHAIJZBIJZBUBUCUDUEUFKZUBUDUCUGUBUDUC
      UGLUBUDMUGUCABCGNOPQRUDUBUFBKUCBSTUA $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d X x $.  $d M x $.
    dif1en.1 $e |- A e. _V $.
    $( If a set ` A ` is equinumerous to the successor of a natural number
       ` M ` , then ` A ` with an element removed is equinumerous to ` M ` .
       Contributed by Jeff Madsen 2-Sep-2009. $)
    dif1en $p |- ( ( M e. om /\ A ~~ suc M /\ X e. A )
                                                -> ( A \ { X } ) ~~ M ) $=
      ( vx com wcel csuc cen wbr w3a csn cdif cv wrex wa cfn breq2 rcla4ev
      isfi sylibr peano2 sylan finfin sylib syl 3adant3 wceq cun wi cin c0
      incom difdisj eqtri unen an4s mpanl2 expcom cvv visset en2sn mpan2 word
      nnord orddisj syl2an 3ad2antl3 wb difsnid df-suc eqcomi a1i breq12d
      3ad2ant3 adantr ensymg nneneq sylan2 entr syl5bi exp3a syld imp an1rs
      3adantl3 sylbid peano4 biimpd 3ad2antl1 3syld biimprcd sylcom r19.23adva
      mpd ) BFGZABHZIJZCAGZKZACLZMZENZIJZEFOZXBBIJZWPWRXEWSWPWRPAQGZXEWQFGZWRXG
      WPXHWRPAXCIJZEFOXGXIWREWQFXCWQAIRSEATUABUBZUCXGXBQGXEAXAUDEXBTUEUFUGWTXDX
      FEFWTXCFGZPZXDBXCUHZXFXLXDXBXAUIZXCXCLZUIZIJZWQXCHZUHZXMWSWPXKXDXQUJZWRXA
      XOIJZXCXOUKULUHZXTWSXKXDYAYBPZXQXDXBXAUKZULUHZYCXQYDXAXBUKULXBXAUMXAAUNUO
      XDYAYEYBXQXBXCXAXOUPUQURUSWSXCUTGYAEVACXCAUTVBVCXKXCVDYBXCVEXCVFUFVGVHXLX
      QAXRIJZXSWTXQYFVIZXKWSWPYGWRWSXNAXPXRIACVJXPXRUHWSXRXPXCVKVLVMVNVOVPWPWRX
      KYFXSUJZWSWPXKWRYHWPXKPZWRYHXHXKWRYHUJWPXHXKPZWRWQAIJZYHXHWRYKUJXKAWQFVQV
      PYJYKYFXSYJWQXRIJZXSYKYFPXHXRFGYLXSVIXKWQXRVRXCUBVSWQAXRVTWAWBWCXJUCWDWEW
      FWGWPWRXKXSXMUJWSYIXSXMBXCWHWIWJWKXMXFXDBXCXBIRWLWMWNWO $.
      $( [25-Jun-2011] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d X x $.  $d M x $.
    dif1enOLD.1 $e |- A e. _V $.
    $( If a set ` A ` is equinumerous to the successor of natural number
       ` M ` , then ` A ` with an element removed is equinumerous to the
       predecessor of ` M ` .  Contributed by Jeff Madsen 2-Sep-2009. $)
    dif1enOLD $p |- ( ( M e. om /\ A ~~ suc M /\ X e. A )
            -> ( A \ { X } ) ~~ M ) $=
      ( vx com wcel csuc cen wbr csn cdif wa wi cv wceq eleq1 sneq difeq2d
      breq1d imbi2d imbi12d ccrd cfv wss cun snssi undif biimpi uncom syl5reqr
      wn visset snid elndif ax-mp cvv difexg unsnen breq1 mpbiri 3syl con0 wb
      onomeneq cardon onsuci peano2 sylancr suc11 nnon bitrd cardid ensymi
      breq2 mpbii syl6bi entr syl5com fvex sucex ensym sylan ex syl com23
      imp3a vtoclg pm2.43i com12 3impia ) BFGZABHZIJZCAGZACKZLZBIJZWOWLWNMZWRWO
      WSWRNZEOZAGZWSAXAKZLZBIJZNZNWOWTNECAXACPZXBWOXFWTXACAQXGXEWRWSXGXDWQBIXGX
      CWPAXACRSTUAUBXBWLWNXEXBWNWLXEXBAXDUCUDZHZIJZWNWLXENZNXBXCAUEZAXDXCUFZPZX
      JXAAUGXLXCXDUFZAXMXLXOAPXCAUHUIXCXDUJUKXNXJXMXIIJZXAXDGULZXPXAXCGXQXAEUMZ
      UNXAXCAUOUPXDXAAUQGXDUQGDAXCUQURUPZXRUSUPAXMXIIUTVAVBXJWNXKXIAIJZWNXKXJWL
      XIWMIJZXEXTWNMWLYAXHBPZXEWLYAXIWMPZYBXIVCGWMFGYAYCVDWLXIWMVEXHXDVFZVGBVHV
      IXHVCGBVCGYCYBVDWLXHBVJYDBVKVIVLYBXDXHIJXEXHXDXSXDVMVNXHBXDIVOVPVQXIAWMVR
      VSAXIXHXDUCVTWAWBWCWDWEWFWGWHWIWJWK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( A set is finite iff its cardinal is a natural number.  Contributed by
       Jeff Madsen 2-Sep-2009. $)
    ficard $p |- ( A e. B -> ( A e. Fin <-> ( card ` A ) e. om ) ) $=
      ( vx wcel cfn ccrd cfv com cv cen wbr wrex wa wceq carden wi cardnn eqtr
      expcom syl eleq1a syld adantl sylbird r19.23adva isfi syl5ib eqcomd
      mpbid ex ancld breq2 rcla4ev sylibr syl6 impbid ) ABDZAEDZAFGZHDZUQACIZJK
      ZCHLZUTURUQVBUTCHUQVAHDZMVBUSVAFGZNZUTAVABHOVDVFUTPUQVDVFUSVANZUTVDVEVANZ
      VFVGPVAQVFVHVGUSVEVARSTVAHUSUAUBUCUDUECAUFZUGUQUTUTAUSJKZMZURUQUTVJUQUTVJ
      UQUTMUSUSFGZNZVJUTVMUQUTVLUSUSQUHUCAUSBHOUIUJUKVKVCURVBVJCUSHVAUSAJULUMVI
      UNUOUP $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d X x y $.
    $( The cardinality of a non-empty finite set is one greater than the
       cardinality of the set with one element removed.  Contributed by Jeff
       Madsen 2-Sep-2009. $)
    dif1card $p |- ( ( A e. Fin /\ X e. A ) -> ( card ` A ) =
                                      suc ( card ` ( A \ { X } ) ) ) $=
      ( vx vy cfn wcel wa cv cen wbr com wrex ccrd cfv csn cdif csuc wceq isfi
      biimpi adantr wi c0 wn n0i wo nn0suc breq2 biimpd en0 syl6ib pm2.24 syl6
      com23 biimpcd r19.22sdv com12 a1d jaoi syl impcom r19.23adva adantl w3a
      cvv elisset cif breq1 eleq2 3anbi23d difeq1 breq1d imbi12d 0ex elimel
      dif1en dedth cardennn peano2 syl3an3 difexg syl3an1 3exp imp suceq
      eqeq2d biimprd 3adant2 mpd 3impd mpdd 3comr 3expia anassrs syld ) AEFZBAF
      ZGZACHZIJZCKLZAMNZABOZPZMNZQZRZWPXAWQWPXACASTUAWRXAADHZQZIJZDKLZXGWQXAXKU
      BZWPWQAUCRZUDZXLABUEXNWTXKCKWSKFZXNWTXKUBZXOWSUCRZWSXIRZDKLZUFXNXPUBZDWSU
      GXQXTXSXQWTXNXKXQWTXMXNXKUBXQWTAUCIJZXMXQWTYAWSUCAIUHUIAUJUKXMXKULUMUNXSX
      PXNWTXSXKWTXRXJDKXRWTXJWSXIAIUHUOUPUQURUSUTVAVBUTVCWRXJXGDKWPWQXHKFZXJXGU
      BZWQYBGZWPYCYDXJWPXGWQYBXJWPXGUBZYBXJWQYEWPYBXJWQVDZXGWPYFXDXHIJZXGWPAVEF
      ZYFYGUBZAEVFYHYIYBYHAUCVGZXIIJZBYJFZVDZYJXCPZXHIJZUBAUCAYJRZYFYMYGYOYPXJY
      KWQYLYBAYJXIIVHAYJBVIVJYPXDYNXHIAYJXCVKVLVMYJXHBAUCVEVNVOVPVQUTWPYBXJWQYG
      XGUBZWPXJYBWQYQUBZWPXJYBYRWPXJYBVDZYQWQYSXBXIRZYQWPXJXIKFYTYBAXIEVRXHVSVT
      WPYBYTYQUBXJWPYBGZYGYTXGUUAYGXEXHRZYTXGUBWPYBYGUUBUBWPYGYBUUBWPYGYBUUBXDV
      EFYGYBUUBWPXDXHVEVRAXCEWAWBWCUNWDUUBXGYTUUBXFXIXBXEXHWEWFWGUMUNWHWIURWCUN
      WJWKUQWLWMUNVAWNVBWOWI $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d v w x y z $.  $d A x y z $.  $d ps x $.  $d ch x $.  $d th x $.
    $d ta x $.  $d ph v w y z $.
    findcard.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    findcard.2 $e |- ( x = ( y \ { z } ) -> ( ph <-> ch ) ) $.
    findcard.3 $e |- ( x = y -> ( ph <-> th ) ) $.
    findcard.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    findcard.5 $e |- ps $.
    findcard.6 $e |- ( y e. Fin -> ( A. z e. y ch -> th ) ) $.
    $( Schema for induction on the cardinality of a finite set.  The inductive
       hypothesis is that the result is true on the given set with any one
       element removed.  The result is then proven to be true for all finite
       sets.  Contributed by Jeff Madsen, 2-Sep-2009. $)
    findcard $p |- ( A e. Fin -> ta ) $=
      ( vw vv cfn wcel cv wceq wex elex wi cen wbr com wrex isfi wal c0 csuc
      breq2 imbi1d albidv en0 mpbiri sylbi ax-gen wral w3a wa rcla4ev peano2
      sylan sylibr 3adant2 csn cdif visset dif1en 3expa pm2.27 syl cvv difexg
      ax-mp breq1 imbi12d cla4v syl5 r19.21adva imp an1rs 3impa sylc 3exp
      19.21adv cbvalv syl6ibr finds1 19.21bi r19.23aiv eleq1 mpbii com12
      19.23adv mpd ) IRSZFTZIUAZFUBEFIRUCWSXAEFXAWSEXAWTRSZAUDWSEUDXBWTPTZUEUFZ
      PUGUHAPWTUIXDAPUGXCUGSXDAUDZFXEFUJWTUKUEUFZAUDZFUJWTQTZUEUFZAUDZFUJZWTXHU
      LZUEUFZAUDZFUJZPQXCUKUAZXEXGFXPXDXFAXCUKWTUEUMUNUOXCXHUAZXEXJFXQXDXIAXCXH
      WTUEUMUNUOXCXLUAZXEXNFXRXDXMAXCXLWTUEUMUNUOXGFXFWTUKUAZAWTUPXSABNJUQURUSX
      HUGSZXKGTZXLUEUFZDUDZGUJXOXTXKYCGXTXKYBDYARSZCHYAUTZDXTXKYBVAOXTYBYDXKXTY
      BVBZYAXCUEUFZPUGUHZYDXLUGSYBYHXTYGYBPXLUGXCXLYAUEUMVCXHVDVEPYAUIVFVGXTXKY
      BYEXTYBXKYEYFXKYEYFXKCHYAYFHTZYASZVBZYAYIVHZVIZXHUEUFZCUDZCXKYKYNYOCUDXTY
      BYJYNYAXHYIGVJZVKVLYNCVMVNXJYOFYMYAVOSYMVOSYPYAYLVOVPVQWTYMUAXIYNACWTYMXH
      UEVRKVSVTWAWBWCWDWEWFWGWHXNYCFGWTYAUAXMYBADWTYAXLUEVRLVSWIWJWKWLWMURXAXBW
      SAEWTIRWNMVSWOWPWQWR $.
      $( [25-Jun-2011] $) $( [2-Sep-2009] $)
  $}

  ${
    $d w x y z $.  $d A x y z $.  $d ps x $.  $d ch x $.  $d th x $.
    $d ta x $.  $d ph w y z $.
    findcardOLD.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    findcardOLD.2 $e |- ( x = ( y \ { z } ) -> ( ph <-> ch ) ) $.
    findcardOLD.3 $e |- ( x = y -> ( ph <-> th ) ) $.
    findcardOLD.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    findcardOLD.5 $e |- ps $.
    findcardOLD.6 $e |- ( y e. Fin -> ( A. z e. y ch -> th ) ) $.
    $( Schema for induction on the cardinality of a set.  The inductive
       hypothesis is that the result is true on the given set with any one
       element removed.  The result is then proven to be true for all finite
       sets.  Contributed by Jeff Madsen, 2-Sep-2009. $)
    findcardOLD $p |- ( A e. Fin -> ta ) $=
      ( vw cfn wcel ccrd cfv wceq eqid cv wi wal com ficardom c0 csuc eqeq2
      imbi1d albidv wb cvv visset cardeq0 ax-mp sylbi mpbiri ax-gen peano2
      eleq1a ficard syl6ibr syl wral w3a csn cdif wa eleq1 biimprd syl5 imp
      dif1card eqeq1 biimpd anim1d con0 suc11 cardon nnon sylancr biimpac syl6
      com12 mpand imim1d ex difexg fveq2 eqeq1d imbi12d cla4v syl7 3imp
      r19.21aiv 3expd com13 mpdd com23 19.21adv cbvalv finds cla4gv mpd mpi )
      IQRZISTZXIUAZEXIUBXHFUCZSTZXIUAZAUDZFUEZXJEUDZXHXIUFRXOIUGXLGUCZUAZAUDZFU
      EXLUHUAZAUDZFUEXLPUCZUAZAUDZFUEZXLYBUIZUAZAUDZFUEZXOGPXIXQUHUAZXSYAFYJXRX
      TAXQUHXLUJUKULXQYBUAZXSYDFYKXRYCAXQYBXLUJUKULXQYFUAZXSYHFYLXRYGAXQYFXLUJU
      KULXQXIUAZXSXNFYMXRXMAXQXIXLUJUKULYAFXTABNXTXKUHUAZABUMXKUNRXTYNUMFUOXKUN
      UPUQJURUSUTYBUFRZYEXQSTZYFUAZDUDZGUEYIYOYEYRGYOYQYEDYOYQXQQRZYEDUDZYOYFUF
      RZYQYSUDYBVAZUUAYQYPUFRZYSYFUFYPVBXQUNRZYSUUCUMGUOZXQUNVCUQZVDVEYSYQYOYTY
      SYQYOYEDYSCHXQVFDYQYOYEVGZOUUGCHXQYQYOYEHUCZXQRZCUDZYQYOXQUUHVHZVIZSTZYBU
      AZCUDZUUJYEYQYOUUOUUJUDYQYOVJZUUIUUNCUUPYSUUIUUNYQYOYSYQUUAYSYOYQUUAUUCYS
      YQUUCUUAYPYFUFVKVLUUFVDUUBVMVNYSUUIVJZUUPUUNUUQYPUUMUIZUAZUUPUUNUDXQUUHVO
      UUSUUPUURYFUAZYOVJUUNUUSYQUUTYOUUSYQUUTYPUURYFVPVQVRYOUUTUUNUUMVSRYBVSRUU
      TUUNUMYOUUMYBVTUULWAYBWBWCWDWEVEWFWGWHWIYDUUOFUULUUDUULUNRUUEXQUUKUNWJUQX
      KUULUAZYCUUNACUVAXLUUMYBXKUULSWKWLKWMWNWOWPWQVMWRWSWTXAXBYHYRFGXKXQUAZYGY
      QADUVBXLYPYFXKXQSWKWLLWMXCVDXDVEXNXPFIQXKIUAZXMXJAEUVCXLXIXIXKISWKWLMWMXE
      XFXG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.
    $( The Cartesian product of two finite sets is finite.  Contributed by Jeff
       Madsen 2-Sep-2009. $)
    fixp $p |- ( ( A e. Fin /\ B e. Fin ) -> ( A X. B ) e. Fin ) $=
      ( vx vy vz vw cfn wcel cxp cv wi c0 csn cdif wceq xpeq1 eleq1d imbi2d
      xp0r emfin eqeltri a1i wral wa wex wn sneq difeq2d syl rcla4v adantl
      pm2.27 ad2antlr cen wbr wb cvv c2nd cres wf1o f1oeng snex xpexg mpan
      visset 2ndconst ax-mp sylancl enfi mpdan ibir cun difsnid xpundir
      syl5eqr biimpd unfi syl5 mpan2d 3syld ex 19.23adv neq0 syl5ib syl6eqel
      a1d pm2.61d2 com23 findcard imp ) AGHBGHZABIZGHZWKCJZBIZGHZKWKLBIZGHZKWKD
      JZEJZMZNZBIZGHZKZWKWSBIZGHZKWKWMKCDEAWNLOZWPWRWKXHWOWQGWNLBPQRWNXBOZWPXDW
      KXIWOXCGWNXBBPQRWNWSOZWPXGWKXJWOXFGWNWSBPQRWNAOZWPWMWKXKWOWLGWNABPQRWRWKW
      QLGBSTUAZUBWSGHZWKXEEWSUCZXGXMWKXNXGKZXMWKUDZWSLOZXOXPFJZWSHZFUEXOXQUFXPX
      SXOFXPXSXOXPXSUDZXNWKWSXRMZNZBIZGHZKZYDXGXSXNYEKXPXEYEEXRWSWTXROZXDYDWKYF
      XCYCGYFXBYBOXCYCOYFXAYAWSWTXRUGUHXBYBBPUIQRUJUKWKYEYDKXMXSWKYDULUMXTYDYAB
      IZGHZXGWKYHXMXSWKYHWKYGBUNUOZYHWKUPYGUQHZYGBURYGUSZUTZYIWKYGBUQYKVAYAUQHW
      KYJXRVBYABUQGVCVDXRUQHYLFVEXRBUQVFVGVHYGBGVIVJVKUMXTYCYGVLZGHZXGYDYHUDXSY
      NXGKXPXSYNXGXSYMXFGXSYBYAVLZBIZXFYMXSYOWSOYPXFOWSXRVMYOWSBPUIYBYABVNVOQVP
      UKYCYGVQVRVSVTWAWBFWSWCWDXQXGXNXQXFWQGWSLBPXLWEWFWGWAWHWIWJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $( Multiplication of both sides of 'less than or equal to' by a nonnegative
       number.  (Contributed by Paul Chapman, 7-Sep-2007.) $)
    lemul2aALT $p |- ( ( A e. RR /\ B e. RR /\ ( C e. RR /\ 0 <_ C ) ) ->
                        ( A <_ B -> ( C x. A ) <_ ( C x. B ) ) ) $=
      ( cr wcel cc0 cle wbr wa w3a cmul co lemul2a ex ) ADEBDECDEFCGHIJABGHCAKL
      CBKLGHABCMN $.
      $( [1-Dec-2010] $) $( [7-Sep-2007] $)
  $}

  ${
    $d M m $.
    $( A finite interval of integers with one element.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzsn $p |- ( M e. ZZ -> ( M ... M ) = { M } ) $=
      ( vm cz wcel cfz co csn cv wceq elfz1eq eleq1 elfz3 syl5cbir impbid2
      elsn syl6bbr eqrdv ) ACDZBAAEFZAGZRBHZSDZUAAIZUATDRUBUCUAAJUCUBASDRUAASKA
      LMNBAOPQ $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d M m $.
    $( A finite interval of integers with two elements.  (Contributed by Jeff
       Madsen, 2-Sep-2009 ) $)
    fzpr $p |- ( M e. ZZ -> ( M ... ( M + 1 ) ) = { M , ( M + 1 ) } ) $=
      ( vm cz wcel c1 caddc co cfz cpr cv wceq wo cuz cfv wb uzid elfzp1 syl
      csn fzsn eleq2d elsn syl6bb orbi1d bitrd visset elpr syl6bbr eqrdv ) ACDZ
      BAAEFGZHGZAUKIZUJBJZULDZUNAKZUNUKKZLZUNUMDUJUOUNAAHGZDZUQLZURUJAAMNDUOVAO
      APUNAAQRUJUTUPUQUJUTUNASZDUPUJUSVBUNATUABAUBUCUDUEUNAUKBUFUGUHUI $.
      $( [1-Dec-2010] $)  $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The class difference of ` RR ` and a closed interval.  (Contributed by
       FL, 18-Jun-2007.) $)
    cdrci $p |- ( ( A e. RR /\ B e. RR ) -> ( RR \ ( A [,] B ) ) =
      U. { ( -oo (,) A ) , ( B (,) +oo ) } ) $=
      ( vx cr wcel wa cicc co cdif cmnf cioo cpnf cpr cuni cv wn cun wo cxr
      cle wbr w3a wb elicc1 rexr syl2an adantr notbid wi pm2.24d adantl clt
      ad2antlr mnflt ltnle bicomd pm3.27 simpll sylanc biimpa 3jca ad2antrr
      mnfxr jctil elioo1 syl mpbird ex adantll ltpnf pnfxr jctir sylbird
      orim12d ianor syl5ib jaod 3anass notbii sylbid expimpd elun syl6ibr
      ioossre unssi sseli elioo2 syl5 ax-mp biimpd a1i com13 3impd xrltnle
      com3l com14 imp sylibr intnand anim12i jca impbid cvv wceq oprex pm3.2i
      uniprg eqcomd eleq2d bitrd eldif syl5bb eqrdv ) ADEZBDEZFZCDABGHZIZJAKHZB
      LKHZMNZXPCOZDEZYBXQEZPZFZYBYAEZYBXREXPYFYBXSXTQZEZYGXPYFYIXPYFYBXSEZYBXTE
      ZRZYIXPYCYEYLXPYCFZYEYBSEZAYBTUAZYBBTUAZUBZPZYLYMYDYQXPYDYQUCZYCASEZBSEZY
      SXNXOABYBUDZAUEZBUEZUFUGUHYMYNYOYPFZFZPZYLYRYMYNPZUUEPZRYLUUGYMUUHYLUUIYC
      UUHYLUIXPYCYNYLYBUEZUJUKYMYOPZYPPZRZYLUUIYMUUKYJUULYKYMUUKYJYMUUKFZYJYNJY
      BULUAZYBAULUAZUBZUUNYNUUOUUPYCYNXPUUKUUJUMYCUUOXPUUKYBUNUMYMUUKUUPYCXNUUK
      UUPUCYMYCXNFZUUPUUKYBAUOZUPXPYCUQXNXOYCURUSUTVAUUNJSEZYTFYJUUQUCUUNYTUUTX
      PYTYCUUKXNYTXOUUCUGVBVCVDJAYBVEVFVGVHYMUULBYBULUAZYKXOYCUVAUULUCXNBYBUOVI
      YMUVAYKYMUVAFZYKYNUVAYBLULUAZUBZUVBYNUVAUVCYCYNXPUVAUUJUMYMUVAUQYCUVCXPUV
      AYBVJUMVAUVBUUALSEZFZYKUVDUCZUVBUUAUVEXPUUAYCUVAXOUUAXNUUDUKZVBVKVLBLYBVE
      ZVFVGVHVMVNYOYPVOZVPVQYNUUEVOVPYQUUFYNYOYPVRVSZVPVTWAYBXSXTWBZWCXPYIYFXPY
      IFZYCYEYIYCXPYHDYBXSXTDJAWDBLWDWEWFUKUVMYEYRUVMUUGYRUVMUUEYNUVMUUMUUIXPYI
      UUMXPYLUUMYIXPYJUUKYKUULXPYJYCUUOUUPUBZUUKXNYJUVNUCZXOUUTXNUVOUIVCUUTYTUV
      OXNUUTYTUVOJAYBWGVHUUCWHWIUGXPYCUUOUUPUUKXNYCUUOUUPUUKUIZUIUIXOUUOYCXNUVP
      YCXNUVPUIUIUUOYCXNUVPUURUUPUUKUUSWJVHWKWLUGWMVTXPYKUVDUULXPUVFUVGXPUUAUVE
      UVHVKVLUVIVFXPYNUVAUVCUULXOYNUVAUVCUULUIUIUIZXNXOUUAUVQUUDUVCYNUVAUUAUULY
      NUVAUUAUULUIUIUIUVCUUAYNUVAUULUUAYNUVAUULUIUUAYNFUVAUULBYBWNWJVHWOWKWPVFU
      KWMVTVNUVLVPWQUVJWRWSUVKWRUVMYTUUAFZYEYRUCXPUVRYIXNYTXOUUAUUCUUDWTUGUVRYD
      YQUUBUHVFVGXAVHXBXPYHYAYBXPXSXCEZXTXCEZFZYHYAXDUWAXPUVSUVTJAKXEBLKXEXFWKU
      WAYAYHXSXTXCXCXGXHVFXIXJYBDXQXKXLXM $.
      $( [1-Dec-2010] $) $( [7-Jun-2007] $)
  $}

  $( Closed intervals are closed sets of the standard topology on ` RR ` .
     (Contributed by FL, 14-Sep-2007.) $)
  clicls $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) e.
        ( Clsd ` ( topGen ` ran (,) ) ) ) $=
    ( cr wcel wa cicc co cioo crn ctg cfv ccld cdif cmnf cpnf cpr cuni cdrci
    ctop wss retop iooretop pm3.2i oprex prss mpbi a1i uniopn syl eqeltrd wb
    iccssre jctil uniretop eqcomi iscld2 mpbird ) ACDBCDEZABFGZHIJKZLKDZCUSMZUT
    DZURVBNAHGZBOHGZPZQZUTABRURUTSDZVFUTTZEZVGUTDVJURVHVIUAVDUTDZVEUTDZEVIVKVLN
    AUBBOUBUCVDVEUTNAHUDBOHUDUEUFUCUGVFUTUHUIUJURVHUSCTZEVAVCUKURVMVHABULUAUMUS
    UTCUTQCUNUOUPUIUQ $.
    $( [1-Dec-2010] $) $( [14-Sep-2007] $)

  $( A closed interval is a closed set.  (Contributed by FL, 28-Jan-2009.) $)
  clint3 $p |- ( ( A e. RR /\ B e. RR ) -> ( A [,] B ) e.
     ( Clsd ` ( topGen ` ran (,) ) ) ) $=
    ( cr wcel wa cicc co wss cdif cioo crn ctg cfv ccld iccssre cmnf cpnf cpr
    cuni cdrci ctop iooretop pm3.2i wb a1i prssg syl mpbii retop jctil uniopn
    eqeltrd jca uniretop eqcomi iscld ax-mp sylibr ) ACDBCDEZABFGZCHZCUTIZJKLMZ
    DZEZUTVCNMDZUSVAVDABOUSVBPAJGZBQJGZRZSZVCABTUSVCUADZVIVCHZEVJVCDUSVLVKUSVGV
    CDZVHVCDZEZVLVMVNPAUBBQUBUCZUSVOVOVLUDVOUSVPUEVGVHVCVCVCUFUGUHUIUJVIVCUKUGU
    LUMVKVFVEUDUIUTVCCVCSCUNUOUPUQUR $.
    $( [1-Dec-2010] $) $( [5-Jan-2009] $)

  ${
    basmetres.1 $e |- D = ( C |` ( Y X. Y ) ) $.
    basmetres.2 $e |- X = dom dom C $.
    $( The base set of a submetric.  (Contributed by Jeff Hankins,
       30-Aug-2009.) $)
    basmetres $p |- ( ( C e. Met /\ Y C_ X ) -> Y = dom dom D ) $=
      ( cme wcel wss wa cxp cdm cin wceq xpss12 anidms df-ss sylib dmeqd
      adantl dmxpid syl6req cr wf metf fdm syl ineq2d eqcomd adantr eqtrd cres
      dmeqi dmres eqtri syl6eqr ) AGHZDCIZJZDDDKZALZMZLZBLZLUSDUTCCKZMZLZVCUSVG
      UTLZDURVGVHNUQURVFUTURUTVEIZVFUTNURVIDCDCOPUTVEQRSTDUAUBUQVGVCNURUQVCVGUQ
      VBVFUQVAVEUTUQVEUCAUDVAVENACFUEVEUCAUFUGUHSUIUJUKVDVBVDAUTULZLVBBVJEUMAUT
      UNUOUMUP $.
      $( [1-Dec-2010] $) $( [21-Aug-2009] $)
  $}

  $( A poset is antisymmetric and reflexive.  (Contributed by FL,
     3-Aug-2009.) $)
  posanref $p |- ( R e. Poset -> ( R i^i `' R ) = ( _I |` U. U. R ) ) $=
    ( cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a isps ibi 3simp3d
    ) ABCZADZAAEAFZAAGHIAJJKLZQRSTMBANOP $.
    $( [1-Dec-2010] $) $( [2-Aug-2009] $)

  $( A poset is transitive.  (Contributed by FL, 3-Aug-2009.) $)
  postr $p |- ( R e. Poset -> ( R o. R ) C_ R ) $=
    ( cps wcel wrel ccom wss ccnv cin cid cuni cres wceq w3a isps ibi 3simp2d
    ) ABCZADZAAEAFZAAGHIAJJKLZQRSTMBANOP $.
    $( [1-Dec-2010] $) $( [2-Aug-2009] $)

  ${
    $d A f x y $.  $d B f x y $.  $d f ph $.  $d ps y $.
    ac6sg.1 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( ~ ac6s with the first premise turned into a antecedent.  (Contributed by
       FL, 3-Aug-2009.) $)
    ac6sg $p |- ( A e. _V -> ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) ) $=
      ( cvv wcel wrex wral cv wf wa wex wi c0 cif wceq raleq1 feq2 anbi12d
      exbidv imbi12d 0ex elimel ac6s dedth ) EIJZADFKZCELZEFGMZNZBCELZOZGPZQUKC
      UJERSZLZURFUMNZBCURLZOZGPZQEREURTZULUSUQVCUKCEURUAVDUPVBGVDUNUTUOVAEURFUM
      UBBCEURUAUCUDUEABCDURFGERIUFUGHUHUI $.
      $( [1-Dec-2010] $) $( [2-Aug-2009] $)
  $}

