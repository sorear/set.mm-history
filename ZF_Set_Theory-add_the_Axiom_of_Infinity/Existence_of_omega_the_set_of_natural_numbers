$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Existence of omega (the set of natural numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The existence of omega (the class of natural numbers).  Axiom 7 of
       [TakeutiZaring] p. 43.  This theorem is proved assuming the Axiom of
       Infinity and in fact is equivalent to it, as shown by the reverse
       derivation ~ inf0 .

       A finitist (someone who doesn't believe in infinity) could, without
       contradiction, replace the Axiom of Infinity by its denial
       ` -. om e. _V ` ; this would lead to ` om = _V ` (the universe of all
       sets) by ~ fineqv .  The finitist could still develop natural number,
       integer, and rational number arithmetic but would be denied the real
       numbers (as well as much of the rest of mathematics).  In deference to
       the finitist, much of our development is done, when possible, without
       invoking the Axiom of Infinity; an example is Peano's axioms ~ peano1
       through ~ peano5 (which many textbooks prove more easily assuming
       Infinity). $)
    omex $p |- om e. _V $=
      ( vx vy com cv wss wex cvv wcel c0 csuc wral wa zfinf2 wi ax-1 ralimi2
      peano5 sylan2 eximi ax-mp visset ssex 19.23aiv ) CADZEZAFZCGHZIUDHZBDZJUD
      HZBUDKZLZAFUFABMULUEAUKUHUIUDHUJNZBCKUEUJUMBUDCUMUICHOPBUDQRSTUEUGACUDAUA
      UBUCT $.
      $( [6-Aug-1994] $)
  $}

  ${
    $d x y z w $.
    $( The first version of the Axiom of Infinity ~ ax-inf proved from the
       second version ~ ax-inf2 .  Note that we didn't use ~ ax-reg , unlike
       the other direction ~ axinf2 . $)
    axinf $p |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $=
      ( omex inf0 ) ABCDEF $.
      $( [24-Apr-2009] $)
  $}

  ${
    $d x y z w $.
    $( The statement "there exists a set that is a proper subset of its union"
       is equivalent to the Axiom of Infinity (see theorem ~ infeq5 ).  This
       provides us with a very compact way to express of the Axiom of Infinity
       using only elementary symbols. $)
    inf5 $p |- E. x x C. U. x $=
      ( cv cuni wpss wex com cvv wcel omex infeq5 mpbir ) ABZLCDAEFGHIAJK $.
      $( [3-Jun-2005] $)
  $}

  $( Omega is an ordinal number.  (Revised by Mario Carneiro, 30-Jan-2013.) $)
  omelon $p |- om e. On $=
    ( com cvv wcel con0 omex omelon2 ax-mp ) ABCADCEFG $.
    $( [30-Jan-2013] $) $( [10-May-1998] $)

  $( Omega is an ordinal number. $)
  omelonOLD $p |- om e. On $=
    ( com con0 wcel wceq cvv omex onprc eleq1 mtbiri mt2 omon ori mt3 ) ABCZABD
    ZOAECZFOPBECGABEHIJNOKLM $.
    $( [10-May-1998] $)

  ${
    $d x y z $.
    $( The class of natural numbers omega can be defined as the smallest
       "inductive set," which is valid provided we assume the Axiom of
       Infinity.  Definition 6.3 of [Eisenberg] p. 82. $)
    dfom3 $p |- om = |^| { x | ( (/) e. x /\ A. y e. x suc y e. x ) } $=
      ( vz com c0 cv wcel csuc wral wa cab cint wi wss 0ex elintab simpl
      mpgbir wal wceq suceq eleq1d rcla4cv adantl a2i alimi visset sucex
      3imtr4i a1i rgen peano5 mp2an peano1 peano2 omex eleq2 raleqbi1dv
      anbi12d imbi12d cla4v sylbi mp2ani ssriv eqssi ) DEAFZGZBFZHZVFGZBVFIZJZA
      KLZEVMGZCFZVMGZVOHZVMGZMZCDIDVMNVNVLVGMAVLAEOPVGVKQRVSCDVSVODGZVLVOVFGZMZ
      ASZVLVQVFGZMZASVPVRWBWEAVLWAWDVKWAWDMVGVJWDBVOVFVHVOTVIVQVFVHVOUAUBUCUDUE
      UFVLAVOCUGZPZVLAVQVOWFUHPUIUJUKCVMULUMCVMDVPEDGZVIDGZBDIZVTUNWIBDVHUOUKVP
      WCWHWJJZVTMZWGWBWLADUPVFDTZVLWKWAVTWMVGWHVKWJVFDEUQVJWIBVFDVFDVIUQURUSVFD
      VOUQUTVAVBVCVDVE $.
      $( [27-Jun-2004] $) $( [6-Aug-1994] $)
  $}

  ${
    $d A x $.
    $( A simplification of ~ elom assuming the Axiom of Infinity. $)
    elom3 $p |- ( A e. om <-> A. x ( Lim x -> A e. x ) ) $=
      ( com wcel cv wlim wi wal word wa elomg ibi simprd limom omex wceq limeq
      eleq2 imbi12d cla4v mpi impbii ) BCDZAEZFZBUDDZGZAHZUCBIZUHUCUIUHJABCKLMU
      HCFZUCNUGUJUCGACOUDCPUEUJUFUCUDCQUDCBRSTUAUB $.
      $( [30-May-2003] $)
  $}

  ${
    $d x y $.
    $( A simplification of ~ df-om assuming the Axiom of Infinity. $)
    dfom4 $p |- om = { x | A. y ( Lim y -> x e. y ) } $=
      ( cv wlim wcel wi wal com elom3 abbi2i ) BCZDACZKEFBGAHBLIJ $.
      $( [30-May-2003] $)
  $}

  ${
    $d x y $.
    $( ` om ` is the smallest limit ordinal and can be defined as such
       (although the Axiom of Infinity is needed to ensure that at least one
       limit ordinal exists).  (Contributed by Mario Carneiro, 2-Feb-2013.) $)
    dfom5 $p |- om = |^| { x | Lim x } $=
      ( vy com cv wlim cab cint wcel wel wi wal elom3 visset elintab bitr4i
      eqriv ) BCADEZAFGZBDZCHQBAIJAKSRHASLQASBMNOP $.
      $( [2-Feb-2013] $)
  $}

  $( Ordinal addition is not commutative.  This theorem shows a
     counterexample.  Remark in [TakeutiZaring] p. 60. $)
  oancom $p |- ( 1o +o om ) =/= ( om +o 1o ) $=
    ( c1o com coa co wcel wne con0 wceq omelon 1onn oaabslem mp2an csuc omex
    sucid oa1suc ax-mp eleqtrri eqeltri wss wa wb 1on oacl onelpss simprbi ) AB
    CDZBACDZEZUGUHFZUGBUHBGEZABEUGBHIJAKLBBMZUHBNOUKUHULHIBPQRSUIUGUHTZUJUGGEZU
    HGEZUIUMUJUAUBAGEZUKUNUCIABUDLUKUPUOIUCBAUDLUGUHUELUFQ $.
    $( [10-Dec-2004] $)

  $( A set is finite iff it is strictly dominated by the class of natural
     number.  Theorem 42 of [Suppes] p. 151.  The Axiom of Infinity is used for
     the forward implication.  (Contributed by FL, 16-Apr-2011.) $)
  isfinite3 $p |- ( A e. Fin <-> A ~< om ) $=
    ( cfn wcel com csdm wbr cdom cen wn wa isfinite1 omex ensym con3i anim2i
    brsdom sylibr syl isfinite2 impbii ) ABCZADEFZUAADGFZDAHFZIZJZUBAKUFUCADHFZ
    IZJUBUEUHUCUGUDADLMNOADPQRAST $.
    $( [16-Apr-2011] $)

  $( A set is strictly dominated by the class of natural numbers iff it is
     finite.  Theorem 42 of [Suppes] p. 151.  This theorem provides two
     equivalent ways to express " ` A ` is finite."  The Axiom of Infinity is
     used for the reverse implication.  Obsolete as of 2-Oct-2013; use
     ~ isfinite3 instead. $)
  isfiniteOLD $p |- ( A ~< om <-> A e. Fin ) $=
    ( cfn wcel com csdm wbr isfinite3 bicomi ) ABCADEFAGH $.
    $( [22-Sep-2008] $) $( [24-Apr-2004] $)

  $( A natural number is strictly dominated by the set of natural numbers. $)
  nnsdom $p |- ( A e. om -> A ~< om ) $=
    ( com wcel cdom wbr cen wn wa csdm omsdomnn omex ensym con3i anim2i syl
    brsdom sylibr ) ABCZABDEZABFEZGZHZABIERSBAFEZGZHUBAJUDUASTUCABKLMNOABPQ $.
    $( [28-Oct-2003] $)

  ${
    $d x y $.
    $( Omega is equinumerous to a proper subset of itself.  Example 13.2(4) of
       [Eisenberg] p. 216. $)
    omenps $p |- om ~~ ( om \ { (/) } ) $=
      ( com cvv wcel c0 csn cdif cen wbr omex limom limenpsi ax-mp ) ABCAADEFGH
      IABJKL $.
      $( [30-Jul-2003] $)
  $}

  $( The set of natural numbers is equinumerous to its successor. $)
  omensuc $p |- om ~~ suc om $=
    ( com cvv wcel csuc cen wbr omex limom limensuci ax-mp ) ABCAADEFGABHIJ $.
    $( [30-Oct-2003] $)

  ${
    $d x y z A $.
    $( Any unbounded subset of natural numbers is equinumerous to the set of
       all natural numbers.  This version of ~ unbnn eliminates its hypothesis
       by assuming the Axiom of Infinity. $)
    unbnn3 $p |- ( ( A C_ om /\ A. x e. om E. y e. A x e. y ) -> A ~~ om ) $=
      ( vz com wss cv wcel wrex wral cen wbr cvv wi omex ssex wa wceq sseq1
      rexeq ralbidv anbi12d breq1 imbi12d visset unbnn vtoclg exp3a mpcom imp
      ) CEFZAGBGHZBCIZAEJZCEKLZCMHZUKUNUONCEOPUPUKUNUODGZEFZULBUQIZAEJZQZUQEKLZ
      NUKUNQZUONDCMUQCRZVAVCVBUOVDURUKUTUNUQCESVDUSUMAEULBUQCTUAUBUQCEKUCUDABUQ
      DUEUFUGUHUIUJ $.
      $( [4-May-2005] $)
  $}

  ${
    $d w x y z F $.
    $( Using the Axiom of Regularity in the form ~ zfregfr , show that there
       are no infinite descending ` e. ` -chains.  Proposition 7.34 of
       [TakeutiZaring] p. 44.  (Revised by Mario Carneiro, 22-Mar-2013.) $)
    noinfep $p |- E. x e. om ( F ` suc x ) e/ ( F ` x ) $=
      ( vz vy vw cv cep wbr wn com cfv cmpt crn wral wrex csuc wnel cvv wcel
      wfr wss c0 wne omex mptexg ax-mp rnex zfregfr ssid cdm wceq dmmpt fvex
      a1i mprg peano1 ne0i eqnetri dm0rn0 necon3bii mpbi fri mp4an wfn wb rgen
      eqid mptfng fvelrnb wi peano2 fveq2 fvmpt syl fnfvelrn sylancr eqeltrrd
      wel epel eleq1 syl5bb notbid df-nel syl6bbr rcla4cv syl5com eqeq1
      syl5ibcom neleq2 biimpd syl6 com23 syld com12 reximdvai syl5bi r19.23aiv
      ) CFZDFZGHZIZCEJEFZBKZLZMZNZDXEOZAFZPZBKZXHBKZQZAJOZXERSXEGTXEXEUAXEUBUCZ
      XGXDJRSXDRSUDEJXCRUEUFUGXEUHXEUIXDUJZUBUCXNXOJUBXCRSZXOJUKEJEJXCRULXPXBJS
      XBBUMUNZUOUBJSJUBUCUPJUBUQUFURXOUBXEUBXDUSUTVADCXEXERGVBVCXFXMDXEXFWSXESZ
      XMXRXHXDKZWSUKZAJOZXFXMXDJVDZXRYAVEXPEJNYBXPEJXQVFEJXCXDXDVGZVHVAZAJWSXDV
      IUFXFXTXLAJXHJSZXFXTXLVJZYEXFXJWSQZYFYEXJXESXFYGYEXIXDKZXJXEYEXIJSZYHXJUK
      XHVKZEXIXCXJJXDXBXIBVLYCXIBUMVMVNYEYBYIYHXESYDYJJXIXDVOVPVQXAYGCXJXEWRXJU
      KZXAXJWSSZIYGYKWTYLWTCDVRYKYLCDVSWRXJWSVTWAWBXJWSWCWDWEWFYEXTYGXLYEXTWSXK
      UKZYGXLVJYEXSXKUKXTYMEXHXCXKJXDXBXHBVLYCXHBUMVMXSWSXKWGWHYMYGXLWSXKXJWIWJ
      WKWLWMWNWOWPWNWQUF $.
      $( [22-Mar-2013] $) $( [26-Jan-2006] $)
  $}

  ${
    $d x y z F $.
    $( Using the Axiom of Regularity in the form ~ zfregfr , show that there
       are no infinite descending ` e. ` -chains.  Proposition 7.34 of
       [TakeutiZaring] p. 44. $)
    noinfepOLD $p |- ( F Fn om -> E. x e. om -. ( F ` suc x ) e. ( F ` x ) ) $=
      ( vz vy com wfn cv cep wbr wn crn wral wrex csuc cfv wcel cvv c0 wne cdm
      wfun fndm omex syl6eqel fnfun funrnex sylc wceq peano1 eleq2 mpbiri ne0i
      syl dm0rn0 necon3bii sylib wfr zfregfr wa wss ssid fri mpanr1 mpanl2
      syl11anc wi wb fvelrnb adantr peano2 fnfvelrn adantlr simplr jca epel
      eleq1 syl5bb notbid rcla4va syl5ibr syl5 sylan2i com12 reximdva sylbid
      ex com23 r19.23adv mpd ) BEFZCGZDGZHIZJZCBKZLZDWOMZAGZNZBOZWRBOZPZJZAEMZW
      JWOQPZWORSZWQWJBTZQPBUAXEWJXGEQEBUBZUCUDEBUEQBUFUGWJXGEUHZXFXHXIXGRSZXFXI
      RXGPZXJXIXKREPUIXGERUJUKXGRULUMXGRWORBUNUOUPUMXEWOHUQZXFWQWOURXEXLUSWOWOU
      TXFWQWOVADCWOWOQHVBVCVDVEWJWPXDDWOWJWPWLWOPZXDWJWPXMXDVFWJWPUSZXMXAWLUHZA
      EMZXDWJXMXPVGWPAEWLBVHVIXNXOXCAEXOXNWREPZUSXCXQXOXNWSEPZXCWRVJXNXRUSZWTWO
      PZWPUSZXOXCXSXTWPWJXRXTWPEWSBVKVLWJWPXRVMVNYAXCXOWTWLPZJZWNYCCWTWOWKWTUHZ
      WMYBWMWKWLPYDYBCDVOWKWTWLVPVQVRVSXOXBYBXAWLWTUJVRVTWAWBWCWDWEWFWGWHWI $.
      $( [26-Jan-2006] $)
  $}


