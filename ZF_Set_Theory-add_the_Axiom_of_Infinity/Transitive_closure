$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Transitive closure
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z w v u $.  $d y z v A $.  $d y v u F $.
    trcl.1 $e |- A e. _V $.
    trcl.2 $e |- F =
               ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) $.
    trcl.3 $e |- C = U_ y e. om ( F ` y ) $.
    $( For any set ` A ` , show the properties of its transitive closure
       ` C ` .  Similar to Theorem 9.1 of [TakeutiZaring] p. 73 except that we
       show an explicit expression for the transitive closure rather than just
       its existence.  See ~ tz9.1 for an abbreviated version showing
       existence. $)
    trcl $p |- ( A C_ C /\ Tr C /\ A. x ( ( A C_ x /\ Tr x ) -> C C_ x ) ) $=
      ( vv vu wss wtr cv wa wi wal com cfv ciun wrex c0 wcel peano1 cuni cun
      wceq copab crdg cres fveq1i cvv fr0g ax-mp eqtr2i eqimssi fveq2 sseq2d
      rcla4ev mp2an ssiun sseqtr4i dftr2 csuc eliun anbi2i r19.42v bitr4i
      elunii ssun2 fvex uniex unex ax-17 hbopab1 hbrdg hbres hbxfreq hbfv
      hbuni hbun unieq uneq12 mpdan frsucopab mpan2 mpbiri sseld syl5 reximia
      sylbi peano2 eleq2d ex syl r19.23aiv cbvrexv sylibr ax-gen mpgbir wb
      treq mpbir wral sseq1d eqtri sseq1i biimpri adantr uniss df-tr sstr2
      syl5bi anc2li unss syl6ib biimprd syl9r com23 adantld finds2 com12
      r19.21aiv cbviunv iunss bitri 3pm3.2i ) EFMFNZEAOZMZYJNZPZFYJMZQZAREBSBOZ
      GTZUAZFEYQMZBSUBZEYRMUCSUDEUCGTZMZYTUEEUUAUUAUCDOCOZUUCUFZUGZUHZCDUIZEUJZ
      SUKZTZEUCGUUIIULZEUMUDUUJEUHHEUMUUGUNUOZUPUQYSUUBBUCSYPUCUHYQUUAEYPUCGURU
      SUTVABSYQEVBUOJVCYIYRNZUUMKOZLOZUDZUUOYRUDZPZUUNYRUDZQZLRKKLYRVDUUTLUURUU
      NYPVEZGTZUDZBSUBZUUSUURUUPUUOYQUDZPZBSUBZUVDUURUUPUVEBSUBZPUVGUUQUVHUUPBU
      UOSYQVFVGUUPUVEBSVHVIUVFUVCBSUVFUUNYQUFZUDYPSUDZUVCUUNUUOYQVJUVJUVIUVBUUN
      UVJUVIUVBMUVIYQUVIUGZMUVIYQVKUVJUVBUVKUVIUVJUVKUMUDUVBUVKUHYQUVIYPGVLZYQU
      VLVMVNCDKEYPUUEUVKUMGUUNEUDCVOZUUNYPUDCVOZCKYQUVICKYPGCKGUUIICKUUHSCKEUUG
      UUFCDKVPUVMVQUUNSUDZCVOVRVSUVNVTZCKYQUVPWAWBIUUCYQUHUUDUVIUHUUEUVKUHUUCYQ
      WCUUCYQUUDUVIWDWEWFWGZUSWHWIWJWKWLUVDUUNYQUDZBSUBZUUSUVDUUNUUOGTZUDZLSUBZ
      UVSUVCUWBBSUVJUVASUDZUVCUWBQYPWMUWCUVCUWBUWAUVCLUVASUUOUVAUHUVTUVBUUNUUOU
      VAGURWNUTWOWPWQUVRUWABLSYPUUOUHYQUVTUUNYPUUOGURWNWRWSBUUNSYQVFWSWPWTXAFYR
      UHYIUUMXBJFYRXCUOXDYOAYMUUNGTZYJMZKSXEZYNYMUWEKSUVOYMUWEUWEUUAYJMZYQYJMZU
      VBYJMZYMKBUUNUCUHUWDUUAYJUUNUCGURXFUUNYPUHUWDYQYJUUNYPGURXFUUNUVAUHUWDUVB
      YJUUNUVAGURXFYKUWGYLUWGYKUUAEYJUUAUUJEUUKUULXGXHXIXJUVJYLUWHUWIQYKUVJUWHY
      LUWIUWHYLUVKYJMZUVJUWIUWHYLUWHUVIYJMZPUWJUWHYLUWKUWHUVIYJUFZMZYLUWKQYQYJX
      KYLUWLYJMUWMUWKYJXLUVIUWLYJXMXNWPXOYQUVIYJXPXQUVJUWIUWJUVJUVBUVKYJUVQXFXR
      XSXTYAYBYCYDYNKSUWDUAZYJMUWFFUWNYJFYRUWNJBKSYQUWDYPUUNGURYEXGXHKSUWDYJYFY
      GWSWTYH $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d A x y z w v $.
    tz9.1.1 $e |- A e. _V $.
    $( Every set has a transitive closure (smallest transitive extension).
       Theorem 9.1 of [TakeutiZaring] p. 73.  See ~ trcl for an explicit
       expression for the transitive closure.  Apparently open problems are
       whether this theorem can be proved without the Axiom of Infinity; if
       not, then whether it implies Infinity; and if not, what is the
       "property" that Infinity has that the other axioms don't have that is
       weaker than Infinity itself?

       (Added 22-Mar-2011) The following article seems to answer the first
       question, that it can't be proved without Infinity, in the affirmative:
       Mancini, Antonella and Zambella, Domenico (2001).  "A note on recursive
       models of set theories." _Notre Dame Journal of Formal Logic_,
       42(2):109-115.  (Thanks to Scott Fenton.) $)
    tz9.1 $p |- E. x ( A C_ x /\ Tr x
                /\ A. y ( ( A C_ y /\ Tr y ) -> x C_ y ) ) $=
      ( vz vv vw com cv cuni cun wceq copab crdg cres cfv ciun wss wtr wa wi
      wal w3a wex eqid trcl omex fvex iunex sseq2 treq sseq1 imbi2d albidv
      3anbi123d cla4ev ax-mp ) CEHEIZFIGIZUSJKLGFMCNHOZPZQZRZVBSZCBIZRVESTZVBVE
      RZUAZBUBZUCZCAIZRZVKSZVFVKVERZUAZBUBZUCZAUDBEGFCVBUTDUTUEVBUEUFVQVJAVBEHV
      AUGURUTUHUIVKVBLZVLVCVMVDVPVIVKVBCUJVKVBUKVRVOVHBVRVNVGVFVKVBVEULUMUNUOUP
      UQ $.
      $( [22-Mar-2011] $) $( [15-Sep-2003] $)

    $( Alternative expression for the existence of transitive closures
       ~ tz9.1 : the intersection of all transitive sets containing ` A ` is a
       set.  (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    tz9.1c $p |- |^| { x | ( A C_ x /\ Tr x ) } e. _V $=
      ( vw vz vy cv wss wtr wa cab c0 wne cint cvv wcel wex com cuni cun wceq
      copab crdg cres cfv ciun wi wal w3a eqid trcl 3simpa omex fvex iunex
      sseq2 treq anbi12d cla4ev mp2b abn0 mpbir intex mpbi ) BAGZHZVEIZJZAKZLMZ
      VINOPVJVHAQZBDRDGZEGFGZVMSTUAFEUBBUCRUDZUEZUFZHZVPIZVHVPVEHUGAUHZUIVQVRJZ
      VKADFEBVPVNCVNUJVPUJUKVQVRVSULVHVTAVPDRVOUMVLVNUNUOVEVPUAVFVQVGVRVEVPBUPV
      EVPUQURUSUTVHAVAVBVIVCVD $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity (no sethood requirement on
       ` A ` ), with the axiom itself present as an antecedent.  See also
       ~ zfregs .  (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    epfrs $p |- ( ( _E Fr A /\ A =/= (/) ) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vz vy vw c0 wne cep wfr cv cin wceq wrex wcel wex wi n0 csn wss wtr wa
      wal w3a snex tz9.1 snssi anim2i ssin visset snss bitr4i sylib ne0i syl
      inss2 inex1 epfrc mp3an2 wel elin anbi1i anass bitri inss1 sseli ancri
      trel inass incom ineq2i eqtri eleq2i bitr2i sylbi ex syl6 exp3a com34
      imp3a syl5 19.23adv syl5bi com23 imp necon4d anim2d expimpd reximdv2
      exp3acom23 impcom 3adant3 19.23aiv ax-mp ) BFGZBHIZAJZBKZFLZABMZWNCJZBNZC
      OWOWSPZCBQXAXBCWTRZDJZSZXDTZXCEJZSXGTUAXDXGSPEUBZUCZDOXAXBPZDEXCWTUDUEXIX
      JDXEXFXJXHXFXEXJXFXEXAXBXEXAUAZXDBKZFGZXFXBXKWTXLNZXMXKXEXCBSZUAZXNXAXOXE
      WTBUFUGXPXCXLSXNXCXDBUHWTXLCUIUJUKULXLWTUMUNXFWOXMWSWOXMUAXLWPKZFLZAXLMZX
      FWSWOXLBSXMXSXDBUOABXLXDBDUIUPUQURXFXRWRAXLBWPXLNZXRUAZADUSZWPBNZXRUAZUAZ
      XFYCWRUAZYAYBYCUAZXRUAYEXTYGXRWPXDBUTVAYBYCXRVBVCXFYBYDYFXFYBUAZXRWRYCYHW
      QFXQFXFYBWQFGZXQFGZPXFYIYBYJYIXGWQNZEOXFYBYJPZEWQQXFYKYLEYKEAUSZYKUAXFYLY
      KYMWQWPXGWPBVDVEVFXFYMYKYLXFYMYBYKYJXFYMYBYKYJPZXFYMYBUAEDUSZYNXDXGWPVGYO
      YKYJYOYKUAZXGXQNZYJYQXGXDWQKZNYPXQYRXGXQXDBWPKZKYRXDBWPVHYSWQXDBWPVIVJVKV
      LXGXDWQUTVMXQXGUMVNVOVPVQVRVSVTWAWBWCWDWEWFWGWBWHVTWIVTVQWJWKWLWMWLVNWJ
      $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity, which does not require that
       ` A ` be a set.  Axiom 6' of [TakeutiZaring] p. 21.  See also
       ~ epfrs . $)
    zfregs $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( cep wfr c0 wne cv cin wceq wrex zfregfr epfrs mpan ) BCDBEFAGBHEIABJBKA
      BLM $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity, which does not require that
       ` A ` be a set.  Axiom 6' of [TakeutiZaring] p. 21.  The proof makes use
       of a special case of Proposition 9.4 of [TakeutiZaring] p. 75.  See also
       ~ epfrs . $)
    zfregsOLD $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vz vy vw c0 wne cv wcel wex cin wceq wrex n0 csn wss wtr wa wi wal w3a
      snex tz9.1 snssi anim2i ssin visset snss bitr4i sylib ne0i syl inex1
      zfreg2 elin anbi1i anass bitri inss1 sseli ancri trel inass incom ineq2i
      eqtri eleq2i bitr2i sylbi ex syl6 exp3a com34 imp3a syl5 19.23adv syl5bi
      com23 imp necon4d anim2d expimpd reximdv2 impcom 3adant3 19.23aiv ax-mp
      ) BFGCHZBIZCJAHZBKZFLZABMZCBNWIWMCWHOZDHZPZWOQZWNEHZPWRQRWOWRPSETZUAZDJWI
      WMSZDEWNWHUBUCWTXADWPWQXAWSWQWPXAWQWPWIWMWPWIRZWOBKZFGZWQWMXBWHXCIZXDXBWP
      WNBPZRZXEWIXFWPWHBUDUEXGWNXCPXEWNWOBUFWHXCCUGUHUIUJXCWHUKULXDXCWJKZFLZAXC
      MWQWMAXCWOBDUGUMUNWQXIWLAXCBWJXCIZXIRZWJWOIZWJBIZXIRZRZWQXMWLRZXKXLXMRZXI
      RXOXJXQXIWJWOBUOUPXLXMXIUQURWQXLXNXPWQXLRZXIWLXMXRWKFXHFWQXLWKFGZXHFGZSWQ
      XSXLXTXSWRWKIZEJWQXLXTSZEWKNWQYAYBEYAWRWJIZYARWQYBYAYCWKWJWRWJBUSUTVAWQYC
      YAYBWQYCXLYAXTWQYCXLYAXTSZWQYCXLRWRWOIZYDWOWRWJVBYEYAXTYEYARZWRXHIZXTYGWR
      WOWKKZIYFXHYHWRXHWOBWJKZKYHWOBWJVCYIWKWOBWJVDVEVFVGWRWOWKUOVHXHWRUKVIVJVK
      VLVMVNVOVPVQVRVSVTWAWBVQWCVOVOVLWDWEWFWGWFVI $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y A $.
    $( Alternate strong form of the Axiom of Regularity.  Not every element of
       a non-empty class contains some element of that class.  (Contributed by
       Alan Sare, 24-Oct-2011.)  (The proof was shortened by Wolf Lammen,
       27-Sep-2013.) $)
    zfregs2 $p |- ( A =/= (/) -> -. A. x e. A E. y ( y e. A /\ y e. x ) ) $=
      ( c0 wne cv wcel wa wex wn wral wrex wi wal cin wceq zfregs incom eqeq1i
      rexbii sylib disj1 alinexa dfrex2 notnot ralbii sylnibr ) CDEZBFZCGZUIAFZ
      GZHBIZJZJZACKZUMACKUHUNACLZUPJUHUJULJMBNZACLZUQUHCUKOZDPZACLZUSUHUKCOZDPZ
      ACLVBACQVDVAACVCUTDUKCRSTUAVAURACBCUKUBTUAURUNACUJULBUCTUAUNACUDUAUMUOACU
      MUEUFUG $.
      $( [27-Sep-2013] $) $( [24-Oct-2011] $)

    $( Obsolete proof of ~ zfregs2 as of 27-Sep-2013. $)
    zfregs2OLD $p |- ( A =/= (/) -> -. A. x e. A E. y ( y e. A /\ y e. x ) ) $=
      ( c0 wne cv wcel wel wa wex wn wral wrex wi wal cin wceq zfregs incom
      eqeq1i rexbii sylib disj1 alinexa dfrex2 notnot2 notnot1 impbii ralbii
      sylnib ) CDEZBFCGZBAHZIBJZKZKZACLZUNACLUKUOACMZUQKUKULUMKNBOZACMZURUKCAFZ
      PZDQZACMZUTUKVACPZDQZACMVDACRVFVCACVEVBDVACSTUAUBVCUSACBCVAUCUAUBUSUOACUL
      UMBUDUAUBUOACUEUBUPUNACUPUNUNUFUNUGUHUIUJ $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Lemma for ~ en3lp . $)
    en3lplem1 $p |- ( ( A e. B /\ B e. C /\ C e. A ) ->
                    ( x = A -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel w3a cv wceq ctp wa wex simp3 eleq2 syl5ibrcom tpid3g 3ad2ant3
      elex22 sylan ex syld ) CDFZDEFZECFZGZAHZCIZEUFFZBHZCDEJZFUIUFFKBLZUEUHUGU
      DUBUCUDMUFCENOUEUHUKUEEUJFZUHUKUDUBULUCECCDPQBEUJUFRSTUA $.
      $( [28-Oct-2011] $)

    $( Lemma for ~ en3lp . $)
    en3lplem2 $p |- ( ( A e. B /\ B e. C /\ C e. A ) -> ( x e. { A , B , C }
                    -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel w3a cv ctp wel wa wex wceq wo wi en3lplem1 3comr a1d tprot eleq2i
      anbi1i exbii bicomi syl8ib jao sylsyld imp 3coml w3o cab idd dftp2
      syl6ib abid df-3or syl3c ex ) CDFZDEFZECFZGZAHZCDEIZFZBHZVCFZBAJZKZBLZVAV
      DKVBCMZVBDMZNZVIOZVBEMZVIOZVLVNNZVIVAVDVMVAVJVIOVDVKVIOVMABCDEPVAVDVKVEDE
      CIZFZVGKZBLZVIVAVKVTOZVDUSUTURWAABDECPQRVIVTVHVSBVFVRVGVCVQVECDESTUAUBUCU
      DVJVIVKUEUFUGVAVDVOVAVDVNVEECDIZFZVGKZBLZVIVAVNWEOZVDUTURUSWFABECDPUHRWDV
      HBWCVFVGWBVCVEECDSTUAUBUDUGVAVDVPVAVDVJVKVNUIZVPVAVDVBWGAUJZFZWGVAVDVDWIV
      AVDUKVCWHVBACDEULTUMWGAUNUMVJVKVNUOUMUGVLVIVNUEUPUQ $.
      $( [28-Oct-2011] $)

    $( No class has 3-cycle membership loops.  This proof was automatically
       generated from the virtual deduction proof ~ en3lpVD using a translation
       program.  (Contributed by Alan Sare, 24-Oct-2011.) $)
    en3lp $p |- -. ( A e. B /\ B e. C /\ C e. A ) $=
      ( vy vx wcel w3a wn ctp c0 wceq noel eleq2 mtbiri tpid3g nsyl simp3 wne
      cv wel wa wex wral zfregs2 wi wal en3lplem2 19.21aiv df-ral sylibr
      pm2.61ine ) ABFZBCFZCAFZGZHABCIZJUPJKZUNUOUQCUPFZUNUQURCJFCLUPJCMNCAABOPU
      LUMUNQPUPJRDSUPFDETUADUBZEUPUCZUOEDUPUDUOESUPFUSUEZEUFUTUOVAEEDABCUGUHUSE
      UPUIUJPUK $.
      $( [28-Oct-2011] $)
  $}

  ${
    $d x y z A $.
    $( Set (epsilon) induction.  Theorem 5.22 of [TakeutiZaring] p. 21. $)
    setind $p |- ( A. x ( x C_ A -> x e. A ) -> A = _V ) $=
      ( vy cv wss wcel wi wal cvv cdif c0 wceq cin wrex wn ssindif0 sseq1
      eleq1 imbi12d a4v syl5bir eldifn nsyli imp nrexdv zfregs necon1bi syl
      vdif0 sylibr ) ADZBEZUKBFZGZAHZIBJZKLZBILUOCDZUPMKLZCUPNZOUQUOUSCUPUOURUP
      FZUSOUOUSURBFZVAUSURBEZUOVBURBPUNVCVBGACUKURLULVCUMVBUKURBQUKURBRSTUAURIB
      UBUCUDUEUTUPKCUPUFUGUHBUIUJ $.
      $( [17-Sep-2003] $)
  $}

  ${
    $d x A $.
    $( Set (epsilon) induction, stated compactly.  Given as a homework problem
       in 1992 by George Boolos (1940-1996). $)
    setind2 $p |- ( ~P A C_ A -> A = _V ) $=
      ( vx cpw wss cv wcel wi wal cvv wceq dfss2 visset elpw imbi1i albii
      bitri setind sylbi ) ACZADZBEZADZUAAFZGZBHZAIJTUASFZUCGZBHUEBSAKUGUDBUFUB
      UCUAABLMNOPBAQR $.
      $( [17-Sep-2003] $)
  $}

  $c TC $. $( Transitive closure $)

  $( Extend class notation to include the transitive closure function. $)
  ctc $a class TC $.

  ${
    $d u x y $.
    $( The transitive closure function. $)
    df-tc $a |- TC = ( x e. _V |-> |^| { y | ( x C_ y /\ Tr y ) } ) $.
  $}

  ${
    $d A x y $.
    $( Value of the transitive closure function.  (The fact that this
       intersection exists is a non-trivial fact that depends on ~ ax-inf ; see
       ~ tz9.1 .)  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcvalg $p |- ( A e. V -> ( TC ` A ) = |^| { x | ( A C_ x /\ Tr x ) } ) $=
      ( vy cv ctc cfv wss wtr wa cab cint wceq fveq2 sseq1 anbi1d abbidv
      inteqd eqeq12d cvv wcel visset tz9.1c df-tc fvmpt2 mp2an vtoclg ) DEZFGZU
      HAEZHZUJIZJZAKZLZMZBFGZBUJHZULJZAKZLZMDBCUHBMZUIUQUOVAUHBFNVBUNUTVBUMUSAV
      BUKURULUHBUJOPQRSUHTUAUOTUAUPDUBZAUHVCUCDTUOTFDAUDUEUFUG $.
      $( [23-Jun-2013] $)
  $}

  ${
    $d A x y $.
    $( Defining property of the transitive closure function: it contains its
       argument as a subset.  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcid $p |- ( A e. V -> A C_ ( TC ` A ) ) $=
      ( vx wcel cv wss wtr wa cab cint ctc cfv wi wal simpl ax-gen ssintab
      mpbir tcvalg syl5sseqr ) ABDACEZFZUAGZHZCIJZAAKLAUEFUDUBMZCNUFCUBUCOPUDCA
      QRCABST $.
      $( [23-Jun-2013] $)

    $( Defining property of the transitive closure function: it is transitive.
       (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tctr $p |- Tr ( TC ` A ) $=
      ( vx vy cvv wcel ctc cfv wtr cv wss wa cab cint trint visset wceq sseq2
      treq anbi12d elab simprbi mprg wb tcvalg syl mpbiri wn c0 tr0 fvprc
      pm2.61i ) ADEZAFGZHZULUNABIZJZUOHZKZBLZMZHZCIZHZVACUSCUSNVBUSEAVBJZVCURVD
      VCKBVBCOUOVBPUPVDUQVCUOVBAQUOVBRSTUAUBULUMUTPUNVAUCBADUDUMUTRUEUFULUGZUNU
      HHZUIVEUMUHPUNVFUCAFUJUMUHRUEUFUK $.
      $( [23-Jun-2013] $)

    $d B x y $.
    $( Defining property of the transitive closure function: it is a subset of
       any transitive class containing ` A ` .  (Contributed by Mario Carneiro,
       23-Jun-2013.) $)
    tcmin $p |- ( A e. V -> ( ( A C_ B /\ Tr B ) -> ( TC ` A ) C_ B ) ) $=
      ( vy vx wcel wss wtr wa cv cab cint ctc cfv wex cvv fvex tcvalg eleq1d
      mpbii intexab sylibr cin ssin biimpi trin anim12i an4s expcom visset
      inex1 wceq sseq2 treq anbi12d elab intss1 sylbir inss2 syl6ss syl6
      19.23adv syl5com sseq1d sylibrd ) ACFZABGZBHZIZADJZGZVJHZIZDKZLZBGZAMNZBG
      VFAEJZGZVRHZIZEOZVIVPVFWAEKLZPFZWBVFVQPFWDAMQVFVQWCPEACRSTWAEUAUBVIWAVPEV
      IWAAVRBUCZGZWEHZIZVPWAVIWHVSVGVTVHWHVSVGIZWFVTVHIWGWIWFAVRBUDUEVRBUFUGUHU
      IWHVOWEBWHWEVNFVOWEGVMWHDWEVRBEUJUKVJWEULVKWFVLWGVJWEAUMVJWEUNUOUPWEVNUQU
      RVRBUSUTVAVBVCVFVQVOBDACRVDVE $.
      $( [23-Jun-2013] $)
  $}

  ${
    $d A x y $.
    tc2.1 $e |- A e. _V $.
    $( A variant of the definition of the transitive closure function, using
       instead the smallest transitive set containing ` A ` as a member, gives
       almost the same set, except that ` A ` itself must be added because it
       is not usually a member of ` ( TC `` A ) ` (and it is never a member if
       ` A ` is well-founded).  (Contributed by Mario Carneiro,
       23-Jun-2013.) $)
    tc2 $p |- ( ( TC ` A ) u. { A } ) = |^| { x | ( A e. x /\ Tr x ) } $=
      ( ctc cfv csn cun cv wcel wtr wa cab cint wss cvv wceq tcvalg ax-mp trss
      impcom simpr jca ss2abi intss eqsstri wi wal simpl ax-gen elintab mpbir
      snss mpbi unss biimpi mp2an fvex snex unex eleq2 treq anbi12d elab snid
      ssun2 sseli cuni uniun tctr df-tr unisn tcid ssun1 sstri mpbir2an intss1
      eqssi ) BDEZBFZGZBAHZIZWAJZKZALZMZVRWFNZVSWFNZVTWFNZVRBWANZWCKZALZMZWFBOI
      ZVRWMPCABOQRWEWLNWMWFNWDWKAWDWJWCWCWBWJWABSTWBWCUAUBUCWEWLUDRUEBWFIZWHWOW
      DWBUFZAUGWPAWBWCUHUIWDABCUJUKBWFCULUMWGWHKWIVRVSWFUNUOUPVTWEIZWFVTNWQBVTI
      ZVTJZWDWRWSKAVTVRVSBDUQBURUSWAVTPWBWRWCWSWAVTBUTWAVTVAVBVCBVSIWRBCVDVSVTB
      VSVRVEVFRWSVTVGZVTNWTVRVTWTVRVGZVSVGZGZVRVRVSVHXAVRNZXBVRNZXCVRNZVRJXDBVI
      VRVJUMXBBVRBCVKWNBVRNCBOVLRUEXDXEKXFXAXBVRUNUOUPUEVRVSVMVNVTVJUKVOVTWEVPR
      VQ $.
      $( [23-Jun-2013] $)

    $d B x $.
    $( The transitive closure function inherits the subset relation.
       (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcss $p |- ( B C_ A -> ( TC ` B ) C_ ( TC ` A ) ) $=
      ( vx wss ctc cfv cv wtr wa cab cint cvv wcel wceq ssexg mpan2 tcvalg syl
      sstr2 anim1d ss2abdv intss ax-mp syl6sseqr eqsstrd ) BAEZBFGZBDHZEZUIIZJZ
      DKZLZAFGZUGBMNZUHUNOUGAMNZUPCBAMPQDBMRSUGUNAUIEZUKJZDKZLZUOUGUTUMEUNVAEUG
      USULDUGURUJUKBAUITUAUBUTUMUCSUQUOVAOCDAMRUDUEUF $.
      $( [23-Jun-2013] $)

    $( The transitive closure function converts the element relation to the
       subset relation.  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcel $p |- ( B e. A -> ( TC ` B ) C_ ( TC ` A ) ) $=
      ( vx wcel ctc cfv cv wss wtr wa cab cint tcvalg wi ssel com12 trss syl6
      imp3a simpr a1i jcad ss2abdv intss syl cvv wceq ax-mp syl6sseqr eqsstrd
      ) BAEZBFGBDHZIZUMJZKZDLZMZAFGZDBANULURAUMIZUOKZDLZMZUSULVBUQIURVCIULVAUPD
      ULVAUNUOULUTUOUNULUTBUMEZUOUNOUTULVDAUMBPQUOVDUNUMBRQSTVAUOOULUTUOUAUBUCU
      DVBUQUEUFAUGEUSVCUHCDAUGNUIUJUK $.
      $( [23-Jun-2013] $)
  $}

  ${
    $( The transitive closure function is idempotent.  (Contributed by Mario
       Carneiro, 23-Jun-2013.) $)
    tcidm $p |- ( TC ` ( TC ` A ) ) = ( TC ` A ) $=
      ( ctc cfv wss wtr ssid tctr cvv wcel wa wi fvex tcmin ax-mp mp2an tcid
      eqssi ) ABCZBCZRRRDZREZSRDZRFAGRHIZTUAJUBKABLZRRHMNOUCRSDUDRHPNQ $.
      $( [23-Jun-2013] $)
  $}


