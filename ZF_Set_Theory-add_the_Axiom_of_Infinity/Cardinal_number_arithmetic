$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +c $. $( Cardinal number addition $)

  $( Extend class definition to include cardinal number addition. $)
  ccda $a class +c $.

  ${
    $d x y z $.
    $( Define cardinal number addition.  Definition of cardinal sum in
       [Mendelson] p. 258.  See ~ cdavali for its value and a description. $)
    df-cda $a |- +c = ( x e. _V , y e. _V |->
                   ( ( x X. { (/) } ) u. ( y X. { 1o } ) ) ) $.
  $}

  ${
    $d x y A $.  $d y B $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258. $)
    cdaval $p |- ( ( A e. V /\ B e. W ) -> ( A +c B ) =
                    ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) $=
      ( vx vy wcel cvv ccda co c0 csn cxp c1o cun wceq elisset wa p0ex xpexg
      mpan2 snex anim12i unexb sylib cv xpeq1 uneq1d uneq2d df-cda ovmpt2g
      mpd3an3 syl2an ) ACGAHGZBHGZABIJAKLZMZBNLZMZOZPZBDGACQBDQUNUOUTHGZVAUNUOR
      UQHGZUSHGZRVBUNVCUOVDUNUPHGVCSAUPHHTUAUOURHGVDNUBBURHHTUAUCUQUSUDUEEFABHH
      EUFZUPMZFUFZURMZOUTIUQVHOHVEAPVFUQVHVEAUPUGUHVGBPVHUSUQVGBURUGUIEFUJUKULU
      M $.
      $( [15-Sep-2013] $) $( [24-Sep-2004] $)
  $}

  ${
    cdaval.1 $e |- A e. _V $.
    cdaval.2 $e |- B e. _V $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258.  For cardinal arithmetic, we follow Mendelson.  Rather than
       defining operations restricted to cardinal numbers, we use this disjoint
       union operation for addition, while cross product and set exponentiation
       stand in for cardinal multiplication and exponentiation.  Equinumerosity
       and dominance serve the roles of equality and ordering.  If we wanted
       to, we could easily convert our theorems to actual cardinal number
       operations via ~ carden , ~ carddom , and ~ cardsdom .  The advantage of
       Mendelson's approach is that we can directly use many equinumerosity
       theorems that we already have available. $)
    cdavali $p |- ( A +c B ) = ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) $=
      ( cvv wcel ccda co c0 csn cxp c1o cun wceq cdaval mp2an ) AEFBEFABGHAIJKB
      LJKMNCDABEEOP $.
      $( [27-Sep-2004] $)

    $( Cardinal addition dominates union. $)
    uncdadom $p |- ( A u. B ) ~<_ ( A +c B ) $=
      ( cun c0 csn cxp c1o ccda co cdom wbr wa cin wceq cen 0ex xpsnen ensymi
      endom ax-mp con0 1on elisseti pm3.2i xp01disj p0ex xpex snex undom mp2an
      cdavali breqtrri ) ABEZAFGZHZBIGZHZEZABJKLAUQLMZBUSLMZNUQUSOFPUOUTLMVAVBA
      UQQMVAUQACAFCRSTAUQUAUBBUSQMVBUSBDBIDIUCUDUESTBUSUAUBUFABUGAUQBUSAUPCUHUI
      DBURDIUJUIUKULABCDUMUN $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( Cardinal addition is equinumerous to union for disjoint sets. $)
    cdaun $p |- ( ( A i^i B ) = (/) -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( cin c0 wceq ccda co csn cxp c1o cun cen cdavali wbr xp01disj wa 0ex
      xpsnen con0 1on elisseti unen mpanl12 mpan syl5eqbr ) ABEFGZABHIAFJKZBLJK
      ZMZABMZNABCDOUIUJEFGZUHUKULNPZABQUIANPUJBNPUMUHRUNAFCSTBLDLUAUBUCTUIAUJBU
      DUEUFUG $.
      $( [5-Apr-2007] $)
  $}

  ${
    cda1en.1 $e |- A e. _V $.
    $( Cardinal addition with cardinal one (which is the same as ordinal one).
       Used in proof of Theorem 6J of [Enderton] p. 143.  (Revised by Mario
       Carneiro, 21-Feb-2013.) $)
    cda1en $p |- ( -. A e. A -> ( A +c 1o ) ~~ suc A ) $=
      ( wcel wn c0 csn cxp c1o cun ccda co csuc cen cin wceq wbr disjsn
      xp01disj wa 0ex xpsnen con0 1on elisseti ensn1 entr4i unen mpanl12 mpan
      sylbir cdavali df-suc 3brtr4g ) AACDZAEFGZHHFGZIZAAFZIZAHJKALMUNAURNEOZUQ
      USMPZAAQUOUPNEOZUTVAAHRUOAMPUPURMPVBUTSVAAEBTUAUPHURHUBUCUDZHHVCVCUAABUEU
      FUOAUPURUGUHUIUJAHBVCUKAULUM $.
      $( [21-Feb-2013] $) $( [28-Sep-2004] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( Cardinal addition is equinumerous to union for disjoint sets.
       (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaung $p |- ( ( A e. V /\ B e. W /\ ( A i^i B ) = (/) ) ->
                   ( A +c B ) ~~ ( A u. B ) ) $=
      ( vx vy wcel cin c0 wceq ccda co cun cen wbr cv wi ineq1 eqeq1d opreq1
      uneq1 breq12d imbi12d ineq2 opreq2 uneq2 visset cdaun vtocl2g 3impia ) AC
      GBDGABHZIJZABKLZABMZNOZEPZFPZHZIJZUPUQKLZUPUQMZNOZQAUQHZIJZAUQKLZAUQMZNOZ
      QULUOQEFABCDUPAJZUSVDVBVGVHURVCIUPAUQRSVHUTVEVAVFNUPAUQKTUPAUQUAUBUCUQBJZ
      VDULVGUOVIVCUKIUQBAUDSVIVEUMVFUNNUQBAKUEUQBAUFUBUCUPUQEUGFUGUHUIUJ $.
      $( [5-Jun-2009] $)
  $}

  $( 1+1=2 for cardinal number addition.  Theorem *110.643 of _Principia
     Mathematica_, vol.  II, p. 86, which adds the remark, "The above
     proposition is occasionally useful."  Unlike us, Whitehead and Russell
     define cardinal addition on collections of all sets equinumerous to 1 and
     2 (which for us are proper classes unless we restrict them as in
     ~ karden ), but after applying definitions, our theorem is equivalent.
     See also the comment for ~ pm54.43 .  The comment for ~ cdavali explains
     why we use ` ~~ ` instead of =. $)
  pm110.643 $p |- ( 1o +c 1o ) ~~ 2o $=
    ( c1o ccda co c0 csn cxp cun c2o cen con0 1on elisseti cdavali cin wceq
    wbr xp01disj wb 0ex xpsnen pm54.43 mp2an mpbi eqbrtri ) AABCADEFZAAEFZGZHIA
    AAJKLZUHMUEUFNDOZUGHIPZAAQUEAIPUFAIPUIUJRADUHSTAAUHUHTUEUFUAUBUCUD $.
    $( [5-Apr-2007] $)

  ${
    cdaen.1 $e |- A e. _V $.
    cdaen.2 $e |- B e. _V $.
    cdaen.3 $e |- C e. _V $.
    cdaen.4 $e |- D e. _V $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258. $)
    cdaen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( cen wbr wa c0 csn cxp c1o cun ccda co cvv wcel wb 0ex xpsnen enen1
      mp2an enen2 bitri con0 1on elisseti cin wceq xp01disj unen mpanr12
      syl2anbr cdavali 3brtr4g ) ABIJZCDIJZKALMZNZCOMZNZPZBVANZDVCNZPZACQRBDQRI
      USVBVFIJZVDVGIJZVEVHIJZUTVIAVFIJZUSASTVBAIJVIVLUAEALEUBUCVBAVFSUDUEBSTVFB
      IJVLUSUAFBLFUBUCVFBASUFUEUGVJCVGIJZUTCSTVDCIJVJVMUAGCOGOUHUIUJZUCVDCVGSUD
      UEDSTVGDIJVMUTUAHDOHVNUCVGDCSUFUEUGVIVJKVBVDUKLULVFVGUKLULVKACUMBDUMVBVFV
      DVGUNUOUPACEGUQBDFHUQUR $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)
  $}

  ${
    $d A w x y z $.  $d B w y z $.  $d C w z $.  $d D w $.  $d W w z $.
    $d X w z $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258.  (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaeng $p |- ( ( ( A e. W /\ B e. X ) /\ ( C e. Y /\ D e. Z ) /\
                     ( A ~~ B /\ C ~~ D ) ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( vz vw vx vy wcel wa cen wbr ccda co wi cv wceq breq1 anbi2d opreq2
      breq1d imbi12d imbi2d breq2 breq2d anbi1d opreq1 visset cdaen vtocl2g
      impcom 3impia ) AEMBFMNZCGMDHMNZABOPZCDOPZNZACQRZBDQRZOPZURUQVAVDSZUQUSIT
      ZJTZOPZNZAVFQRZBVGQRZOPZSZSUQUSCVGOPZNZVBVKOPZSZSUQVESIJCDGHVFCUAZVMVQUQV
      RVIVOVLVPVRVHVNUSVFCVGOUBUCVRVJVBVKOVFCAQUDUEUFUGVGDUAZVQVEUQVSVOVAVPVDVS
      VNUTUSVGDCOUHUCVSVKVCVBOVGDBQUDUIUFUGKTZLTZOPZVHNZVTVFQRZWAVGQRZOPZSAWAOP
      ZVHNZVJWEOPZSVMKLABEFVTAUAZWCWHWFWIWJWBWGVHVTAWAOUBUJWJWDVJWEOVTAVFQUKUEU
      FWABUAZWHVIWIVLWKWGUSVHWABAOUHUJWKWEVKVJOWABVGQUKUIUFVTWAVFVGKULLULIULJUL
      UMUNUNUOUP $.
      $( [5-Jun-2009] $)
  $}

  ${
    cda0en.1 $e |- A e. _V $.
    $( Cardinal addition with cardinal zero (the empty set).  Part (a1) of
       proof of Theorem 6J of [Enderton] p. 143. $)
    cda0en $p |- ( A +c (/) ) ~~ A $=
      ( c0 ccda co csn cxp cen c1o cun 0ex cdavali xp0r uneq2i un0 3eqtri
      xpsnen eqbrtri ) ACDEZACFGZAHSTCIFZGZJTCJTACBKLUBCTUAMNTOPACBKQR $.
      $( [27-Sep-2004] $)

    $( One times a cardinal number. $)
    xp1en $p |- ( A X. 1o ) ~~ A $=
      ( c1o cxp c0 csn cen df1o2 xpeq2i 0ex xpsnen eqbrtri ) ACDAEFZDAGCMAHIAEB
      JKL $.
      $( [27-Sep-2004] $)

    $( Two times a cardinal number.  Exercise 4.56(g) of [Mendelson] p. 258. $)
    xp2cda $p |- ( A X. 2o ) = ( A +c A ) $=
      ( c0 csn c1o cun cxp c2o ccda co xpundi cpr df-pr df2o2 df1o2 sneqi
      uneq2i 3eqtr4i xpeq2i cdavali ) ACDZEDZFZGAUAGAUBGFAHGAAIJAUAUBKHUCACUALU
      AUADZFHUCCUAMNUBUDUAEUAOPQRSAABBTR $.
      $( [27-Sep-2004] $)
  $}

  ${
    cdacomen.1 $e |- A e. _V $.
    cdacomen.2 $e |- B e. _V $.
    $( Commutative law for cardinal addition.  Exercise 4.56(c) of [Mendelson]
       p. 258. $)
    cdacomen $p |- ( A +c B ) ~~ ( B +c A ) $=
      ( c0 csn cxp c1o cun ccda co cen cin wceq wbr xp01disj wne 1n0 xpsndisj
      ax-mp wa 0ex xpsnen con0 1on elisseti entr4i unen mpanl12 mp2an uncom
      breqtri cdavali 3brtr4i ) AEFZGZBHFZGZIZBUOGZAUQGZIZABJKBAJKLUSVAUTIZVBLU
      PURMENZVAUTMENZUSVCLOZABPHEQVERAHBESTUPVALOURUTLOVDVEUAVFUPAVACAECUBUCAHC
      HUDUEUFZUCUGURBUTDBHDVGUCBEDUBUCUGUPVAURUTUHUIUJVAUTUKULABCDUMBADCUMUN $.
      $( [24-Jul-2006] $) $( [24-Sep-2004] $)

    ${
      cdaassen.3 $e |- C e. _V $.
      $( Associative law for cardinal addition.  Exercise 4.56(c) of
         [Mendelson] p. 258. $)
      cdaassen $p |- ( ( A +c B ) +c C ) ~~ ( A +c ( B +c C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen wbr cin wceq cvv wcel p0ex xpex snex
        unex xpundir eqeng mp2 con0 1on elisseti xpsnen ensymi xp01disj wa
        pm3.2i undisj1 mpbi unen mp4an enref xpassen xpcomen xpen mp2an entr4i
        entri xpeq1i xpindir xp0r 3eqtr3i unass breqtri 0ex undisj2 cdavali
        opreq1i eqtri opreq2i 3brtr4i ) AGHZIZBJHZIZKZVSIZCWAIZKZVTBVSIZWEKZWAI
        ZKZABLMZCLMZABCLMZLMZNWFVTVSIZWBVSIZKZWEWAIZKZWJWDWQNOZWEWRNOWDWEPGQWQW
        RPGQZWFWSNOWDRSWDWQQWTWCVSVTWBAVSDTUAZBWAEJUBZUAUCZTUAVTWBVSUDWDWQRUEUF
        WRWECWAFXCUAZWEJXEJUGUHUIUJUKWCCULWOWRPGQZWPWRPGQZUMXAXFXGVTWEULZWBWEUL
        UNWOWPWRUOUPZWDWQWEWRUQURWSWOWGWAIZWRKZKZWJWOXKVTVSXBTUAZXJWRWGWABVSETU
        AZXCUAWEWAXEXCUAZUCUCWSWOXJKZWRKZXLNWQXPNOZWRWRNOXAXPWRPGQZWSXQNOWOWONO
        WPXJNOWOWPPZGQWOXJPGQZXRWOXMUSWPBWAVSIZIZXJBWAVSEXCTUTYCBVSWAIZIZXJBYDE
        VSWATXCUAZUABBNOYBYDNOYCYENOBEUSWAVSXCTVABBYBYDEYFVBVCBVSWAETXCUTVDVEVT
        WBPZVSIGVSIXTGYGGVSABULVFVTWBVSVGVSVHVIVTWGULZWOWOWPXJUQURWRXOUSXIXFXJW
        RPZGQZUMXSXFYJXHWGWEPZWAIGWAIYIGYKGWABCULVFWGWEWAVGWAVHVIUNWOXJWRUOUPWQ
        XPWRWRUQURWOXJWRVJVKVTWONOWIXKNOZVTWIPGQWOXKPGQZWJXLNOWOVTXBVTGXBVLUJUK
        WIRSWIXKQYLWHWAWGWEXNXEUCZXCUAWGWEWAUDWIXKRUEUFAWHULYAXFUMYMYAXFYHXHUNW
        OXJWRVMUPVTWOWIXKUQURVDVEWLWCCLMWFWKWCCLABDEVNVOWCCXDFVNVPWNAWHLMWJWMWH
        ALBCEFVNVQAWHDYNVNVPVR $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Cardinal multiplication distributes over cardinal addition.  Theorem
         6I(3) of [Enderton] p. 142. $)
      xpcdaen $p |- ( A X. ( B +c C ) ) ~~ ( ( A X. B ) +c ( A X. C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen p0ex xpex snex unex wbr cin wceq
        xpassen xp01disj xpeq2i xpindi xp0 3eqtr3i unen mp4an xpundi breqtrri
        ensymi cdavali 3brtr4i ) ABGHZIZCJHZIZKZIZABIZUOIZACIZUQIZKZABCLMZIVAVC
        LMNVEUTAUSDUPURBUOEOPCUQFJQZPRPVEAUPIZAURIZKZUTNVBVHNSVDVINSVBVDTGUAVHV
        ITZGUAVEVJNSABUODEOUBACUQDFVGUBVAVCUCAUPURTZIAGIVKGVLGABCUCUDAUPURUEAUF
        UGVBVHVDVIUHUIAUPURUJUKULVFUSABCEFUMUDVAVCABDEPACDFPUMUN $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Sum of exponents law for cardinal arithmetic.  Theorem 6I(4) of
         [Enderton] p. 142. $)
      mapcdaen $p |- ( A ^m ( B +c C ) ) ~~ ( ( A ^m B ) X. ( A ^m C ) ) $=
        ( ccda co cmap c0 csn cxp c1o cun cen cdavali opreq2i cin wceq wbr
        xp01disj p0ex xpex snex mapunen ax-mp enref 0ex xpsnen mapen mp2an
        con0 1on elisseti oprex xpen entri eqbrtri ) ABCGHZIHABJKZLZCMKZLZNZIHZ
        ABIHZACIHZLZOUSVDAIBCEFPQVEAVAIHZAVCIHZLZVHVAVCRJSVEVKOTBCUAVAVCABUTEUB
        UCCVBFMUDUCDUEUFVIVFOTZVJVGOTZVKVHOTAAOTZVABOTVLADUGZBJEUHUIAAVABDEUJUK
        VNVCCOTVMVOCMFMULUMUNUIAAVCCDFUJUKVIVFVJVGABIUOACIUOUPUKUQUR $.
        $( [24-Jul-2006] $) $( [27-Sep-2004] $)

      $( Ordering law for cardinal addition.  Exercise 4.56(f) of [Mendelson]
         p. 258. $)
      cdadom1 $p |- ( A ~<_ B -> ( A +c C ) ~<_ ( B +c C ) ) $=
        ( cdom wbr c0 csn cxp c1o cun ccda co cvv wcel cen wb 0ex xpsnen
        domen1 mp2an domen2 bitri snex xpex domrefg ax-mp wa cin wceq xp01disj
        p0ex undom mpan2 sylbir cdavali 3brtr4g ) ABGHZAIJZKZCLJZKZMZBVAKZVDMZA
        CNOBCNOGUTVBVFGHZVEVGGHZVHAVFGHZUTAPQVBARHVHVJSDAIDTUAVBAVFPUBUCBPQVFBR
        HVJUTSEBIETUAVFBAPUDUCUEVHVDVDGHZVIVDPQVKCVCFLUFUGZVDPUHUIVHVKUJVFVDUKI
        ULVIBCUMVBVFVDVDBVAEUNUGVLVLUOUPUPUQACDFURBCEFURUS $.
        $( [24-Jul-2006] $) $( [28-Sep-2004] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdadom2 $p |- ( A ~<_ B -> ( C +c A ) ~<_ ( C +c B ) ) $=
        ( cdom wbr ccda co cdadom1 cen cdacomen endomtr mpan domentr mpan2
        3syl ) ABGHACIJZBCIJZGHZCAIJZTGHZUBCBIJZGHZABCDEFKUBSLHUAUCCAFDMUBSTNOU
        CTUDLHUEBCEFMUBTUDPQR $.
        $( [28-Sep-2004] $)
    $}

    $( A set is dominated by its cardinal sum with another. $)
    cdadom3 $p |- A ~<_ ( A +c B ) $=
      ( cun cdom wbr ccda co cvv wcel wss ssun1 ssdomg mp2 uncdadom domtr
      mp2an ) AABEZFGZSABHIZFGAUAFGAJKASLTCABMASJNOABCDPASUAQR $.
      $( [28-Sep-2004] $)
  $}

  $( The cardinal sum of two finite sets is finite. $)
  cdafi $p |- ( ( A ~< om /\ B ~< om ) -> ( A +c B ) ~< om ) $=
    ( com csdm wbr wa ccda co c0 csn cxp c1o cun cvv wcel wceq sdomex simpld
    cdaval syl2an cen wb con0 0elon xpsneng sylancl sdomen1 syl11anc ibir 1on
    unfi2 eqbrtrd ) ACDEZBCDEZFABGHZAIJKZBLJKZMZCDUMANOZBNOZUOURPUNUMUSCNOZACQR
    ZUNUTVABCQRZABNNSTUMUPCDEZUQCDEZURCDEUNUMVDUMUSUPAUAEZVDUMUBVBUMUSIUCOVFVBU
    DAINUCUEUFUPACNUGUHUIUNVEUNUTUQBUAEZVEUNUBVCUNUTLUCOVGVCUJBLNUCUEUFUQBCNUGU
    HUIUPUQUKTUL $.
    $( [22-Oct-2004] $)

  ${
    $( Any partition of omega into two pieces (which may be disjoint) contains
       an infinite subset.  (Contributed by Mario Carneiro, 11-Feb-2013.) $)
    cdainflem $p |- ( ( A u. B ) ~~ om -> ( A ~~ om \/ B ~~ om ) ) $=
      ( cun com cen wbr csdm wa wn wo unfi2 sdomnen syl con2i ianor cdom cvv
      wcel wss ssun1 relen brrelexi ssexg sylancr ssdomg ee10 domentr mpancom
      anim1i bren2 sylibr ex ssun2 orim12d syl5bi mpd ) ABCZDEFZADGFZBDGFZHZIZA
      DEFZBDEFZJZVAURVAUQDGFURIABKUQDLMNVBUSIZUTIZJURVEUSUTOURVFVCVGVDURVFVCURV
      FHADPFZVFHVCURVHVFAUQPFZURVHURAQRZAUQSZVIURVKUQQRZVJABTZUQDEUAUBZAUQQUCUD
      VMAUQQUEUFAUQDUGUHUIADUJUKULURVGVDURVGHBDPFZVGHVDURVOVGBUQPFZURVOURBQRZBU
      QSZVPURVRVLVQBAUMZVNBUQQUCUDVSBUQQUEUFBUQDUGUHUIBDUJUKULUNUOUP $.
      $( [11-Feb-2013] $)
  $}

  ${
    $d A x y $.
    cdainf.1 $e |- A e. _V $.
    $( A set is infinite iff the cardinal sum with itself is infinite.
       (Revised by Mario Carneiro, 11-Feb-2013.) $)
    cdainf $p |- ( om ~<_ A <-> om ~<_ ( A +c A ) ) $=
      ( vx vy com cdom wbr ccda co cdadom3 domtr mpan2 cv cen wss wa wex oprex
      domen c0 csn cxp cin c1o cun wo visset ensym cdavali ineq2i indi eqtri
      wceq df-ss biimpi syl5reqr breq1d biimpac sylan cdainflem omex inss2
      inex1 breq2 sseq1 anbi12d cla4ev sylancl p0ex xpex sylibr 0ex xpsnen
      domentr snex 1onn elisseti jaoi 3syl 19.23aiv sylbi impbii ) EAFGZEAAHIZF
      GZWCAWDFGWEAABBJEAWDKLWEECMZNGZWFWDOZPZCQWCCEWDAAHRSWIWCCWIWFATUAZUBZUCZW
      FAUDUAZUBZUCZUEZENGZWLENGZWOENGZUFWCWGWFENGZWHWQEWFCUGZUHWHWTWQWHWFWPENWH
      WPWFWDUCZWFXBWFWKWNUEZUCWPWDXCWFAABBUIUJWFWKWNUKULWHXBWFUMWFWDUNUOUPUQURU
      SWLWOUTWRWCWSWREWKFGZWKANGWCWREDMZNGZXEWKOZPZDQZXDWREWLNGZWLWKOZXIWLEVAUH
      WFWKVBXHXJXKPDWLWFWKXAVCXEWLUMXFXJXGXKXEWLENVDXEWLWKVEVFVGVHDEWKAWJBVIVJS
      VKATBVLVMEWKAVNVHWSEWNFGZWNANGWCWSXFXEWNOZPZDQZXLWSEWONGZWOWNOZXOWOEVAUHW
      FWNVBXNXPXQPDWOWFWNXAVCXEWOUMXFXPXMXQXEWOENVDXEWOWNVEVFVGVHDEWNAWMBUDVOVJ
      SVKAUDBUDEVPVQVMEWNAVNVHVRVSVTWAWB $.
      $( [11-Feb-2013] $) $( [22-Oct-2004] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The cardinal and ordinal sums are always equinumerous.  (Contributed by
       Mario Carneiro, 6-Feb-2013.) $)
    onacda $p |- ( ( A e. On /\ B e. On ) -> ( A +o B ) ~~ ( A +c B ) ) $=
      ( vx vy vz con0 wcel wa coa co cv wceq wrex cab ccda cen wbr cun cin c0
      wex wn wi visset weq eqeq1 rexbidv elab eleq1 biimpd word onelon oacl
      sylan2 eloni ordirr 3syl nsyli imp wss oaword1 sseld adantl mtod ex
      exp3acom23 impcom r19.23aiva sylbi com13 con2d imnan sylib elin sylnibr
      nexdv n0 necon1bbii cvv abrexexg cdaung syl3an2 mpd3an3 oarec breqtrrd
      oprex ensym syl simpl csn ciun df-iun elsn rexbii abbii eqtri wral snex
      a1i rgen iunexg mpan2 syl5eqelr simpr enrefg adantr cmpt wf1o wf1 crn wf
      cfv opreq2 eqid fvmpt ad2antrl ad2antll eqeq12d wb simpll ad2ant2lr
      ad2ant2l oacan syl111anc bitrd r19.21aivva wtru tru eqeq2d rcla4ev
      sylibr fmptd ax-mp jctil dff13 ax-17 cbvmpt rnmpt jctir dff1o5 f1oeng
      ensymg sylsyld mpd cdaeng syl222anc entr syl11anc ) AFGZBFGZHZABIJZACKZAD
      KZIJZLZDBMZCNZOJZPQZUUSABOJZPQZUULUVAPQUUKUUSUULPQUUTUUKUUSAUURRZUULPUUIU
      UJAUURSZTLZUUSUVCPQZUUKEKZUVDGZEUAZUBUVEUUKUVHEUUKUVGAGZUVGUURGZHZUVHUUKU
      VJUVKUBUCUVLUBUUKUVKUVJUUIUUJUVKUVJUBZUCUVKUUJUUIUVMUVKUVGUUOLZDBMZUUJUUI
      UVMUCZUCZUUQUVOCUVGEUDCEUEUUPUVNDBUUMUVGUUOUFUGUHUVNUVQDBUVNUUNBGZUVQUVNU
      UJUVRUVPUVNUUIUUJUVRHZUVMUVNUUIUVSHZUVMUVNUVTHUVJUVGUUOGZUVNUVTUWAUBUVNUW
      AUUOUUOGZUVTUVNUWAUWBUVGUUOUUOUIUJUVTUUOFGZUUOUKUWBUBUVSUUIUUNFGZUWCBUUNU
      LZAUUNUMUNUUOUOUUOUPUQURUSUVTUVJUWAUCUVNUVTAUUOUVGUVSUUIUWDAUUOUTUWEAUUNV
      AUNVBVCVDVEVFVFVGVHVIVJUSVKUVJUVKVLVMUVGAUURVNVOVPUVIUVDTEUVDVQVRVMUUJUUI
      UURVSGZUVEUVFDCBUUOFVTAUURFVSWAWBWCCDABWDWEUUSUULABIWFWGWHUUKUUIUUIUWFUUJ
      AAPQZUURBPQZUVBUUIUUJWIZUWIUUKUURDBUUOWJZWKZVSUWKUUMUWJGZDBMZCNUURDCBUWJW
      LUWMUUQCUWLUUPDBCUUOWMWNWOWPUUJUWKVSGZUUIUUJUWJVSGZDBWQUWNUWODBUWOUVRUUOW
      RWSWTDBUWJFVSXAXBVCXCZUUIUUJXDUUIUWGUUJAFXEXFUUKBUUREBAUVGIJZXGZXHZUWHUUK
      BUURUWRXIZUWRXJUURLZHUWSUUKUWTUXAUUKBUURUWRXKZUUMUWRXLZUUNUWRXLZLZCDUEZUC
      ZDBWQCBWQZHUWTUUKUXHUXBUUKUXGCDBBUUKUUMBGZUVRHZHZUXEUXFUXKUXEAUUMIJZUUOLZ
      UXFUXKUXCUXLUXDUUOUXIUXCUXLLUUKUVREUUMUWQUXLBUWRUVGUUMAIXMUWRXNZAUUMIWFXO
      XPUVRUXDUUOLUUKUXIEUUNUWQUUOBUWRUVGUUNAIXMZUXNAUUNIWFXOXQXRUXKUUIUUMFGZUW
      DUXMUXFXSUUIUUJUXJXTUUJUXIUXPUUIUVRBUUMULYAUUJUVRUWDUUIUXIUWEYBAUUMUUNYCY
      DYEUJYFYGUXBYHYGEBUWQUURUWRUVGBGZUWQUURGZUCYGUXQUWQUWQLZUXRUWQXNUXQUXSHUW
      QUUOLZDBMZUXRUXTUXSDUVGBUUNUVGLUUOUWQUWQUUNUVGAIXMYIYJUUQUYACUWQAUVGIWFUU
      MUWQLUUPUXTDBUUMUWQUUOUFUGUHYKXBWSUXNYLYMYNCDBUURUWRYOYKDCBUUOUWREDCBUWQU
      UOUUMUWQGDYPUUMUUOGEYPUXOYQYRYSBUURUWRYTYKUUKUWFUWSBUURPQZUWHUWPUUJUWSUYB
      UCUUIUUJUWSUYBBUURFUWRUUAVEVCBUURVSUUBUUCUUDAAUURBFFVSFUUEUUFUULUUSUVAUUG
      UUH $.
      $( [6-Feb-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The cardinal sum is equinumerous to an ordinal sum of the cardinals.
       (Contributed by Mario Carneiro, 6-Feb-2013.) $)
    cardacda $p |- ( ( ( A e. _V /\ B e. _V ) /\
       ( E. x e. On x ~~ A /\ E. x e. On x ~~ B ) ) ->
         ( A +c B ) ~~ ( ( card ` A ) +o ( card ` B ) ) ) $=
      ( cvv wcel wa cv cen wbr con0 wrex ccda co ccrd cfv coa cardid2 w3a
      cardon jctl cdaeng syl3an1 syl3an2 onacda mp2an entr mpan oprex ensym
      3syl 3expia syl2ani imp ) BDEZCDEZFZAGZBHIAJKZUQCHIAJKZFBCLMZBNOZCNOZPMZH
      IZURUPVABHIZVBCHIZVDUSABQACQUNUOVEVFFZVDUNUOVGRVAVBLMZUTHIZVCUTHIZVDUOUNV
      BJEZUOFZVGVIUOVKCSZTUNVAJEZUNFVLVGVIUNVNBSZTVABVBCJDJDUAUBUCVCVHHIZVIVJVN
      VKVPVOVMVAVBUDUEVCVHUTUFUGVCUTBCLUHUIUJUKULUM $.
      $( [6-Feb-2013] $)
  $}

  $( The cardinal and ordinal sums of finite ordinals are equal.  (Contributed
     by Paul Chapman, 11-Apr-2009.)  (Revised by Mario Carneiro,
     6-Feb-2013.) $)
  nnacda $p |- ( ( A e. om /\ B e. om ) ->
                 ( card ` ( A +c B ) ) = ( A +o B ) ) $=
    ( com wcel wa coa co ccrd cfv ccda cvv cen wbr wceq oprex con0 nnon onacda
    syl2an carden2b mpsyl nnacl cardnn syl eqtr3d ) ACDZBCDZEZABFGZHIZABJGZHIZU
    IUKKDUHUIUKLMZUJULNABJOUFAPDBPDUMUGAQBQABRSUIUKKTUAUHUICDUJUINABUBUIUCUDUE
    $.
    $( [6-Feb-2013] $) $( [11-Apr-2009] $)

  ${
    $d A x $.  $d B x $.
    $( The cardinality of the union of disjoint, finite sets is the ordinal sum
       of their cardinalities.  (Contributed by Paul Chapman, 5-Jun-2009.)
       (Revised by Mario Carneiro, 6-Feb-2013.) $)
    ficardun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( card ` ( A u. B ) ) = ( ( card ` A ) +o ( card ` B ) ) )
        $=
      ( vx cfn wcel cin c0 wceq w3a ccrd cfv coa co cun cen wbr ccda con0 wa
      cv wrex cardon oacl mp2an onacda pm3.2i breq1 rcla4ev cardid2 mp2b cvv
      fvex simpr wi finnum syl anim12i cdaeng syl3an3 3expia an4s mpd mpanl12
      3adant3 cdaung entr syl11anc sylancr wb com ficardom nnacda syl2an
      breq1d mpbid unfi carden2b nnacl cardnn eqtr3d ) ADEZBDEZABFGHZIZAJKZBJKZ
      LMZJKZABNZJKZWGWDWGWIOPZWHWJHZWDWEWFQMZJKZWIOPZWKWDWNWMOPZWMWIOPZWOWGREZW
      GWMOPZSCTZWMOPZCRUAWPWRWSWEREZWFREZWRAUBZBUBZWEWFUCUDXBXCWSXDXEWEWFUEUDUF
      XAWSCWGRWTWGWMOUGUHCWMUIUJWDWMABQMZOPZXFWIOPWQWAWBXGWCWEUKEZWFUKEZWAWBSZX
      GAJULBJULXHXISZXJSXJXGXKXJUMXHWAXIWBXJXGUNXHWASZXIWBSZXJXGXJXLXMWEAOPZWFB
      OPZSXGWAXNWBXOWAWTAOPCRUAXNCAUOCAUIUPWBWTBOPCRUAXOCBUOCBUIUPUQWEAWFBUKDUK
      DURUSUTVAVBVCVDABDDVEWMXFWIVFVGWNWMWIVFVHWAWBWOWKVIWCXJWNWGWIOWAWEVJEZWFV
      JEZWNWGHWBAVKZBVKZWEWFVLVMVNVDVOWAWBWKWLUNZWCXJWIDEXTABVPWGWIDVQUPVDVBWAW
      BWHWGHZWCWAXPXQYAWBXRXSXPXQSWGVJEYAWEWFVRWGVSUPVMVDVT $.
      $( [6-Feb-2013] $) $( [5-Jun-2009] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The cardinality of the union of finite sets is at most the ordinal sum
       of their cardinalities.  (Contributed by Mario Carneiro, 5-Feb-2013.) $)
    ficardun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( card ` ( A u. B ) ) C_ ( ( card ` A ) +o ( card ` B ) ) )
        $=
      ( vx cfn wcel wa cun ccrd cfv cdif coa co undif2 fveq2i wceq diffi cin
      c0 disjdif ficardun mp3an3 sylan2 syl5eqr wss cdom wbr cen cv con0 wrex
      finnum cardid2 3syl difss ssdom2g mpi id syl endomtr ensymg imp domentr
      syl2an syl22anc wb cardon carddom2 mp2an sylib cardidm 3sstr3g oaword
      mp3an adantl eqsstrd ) ADEZBDEZFZABGZHIZAHIZBAJZHIZKLZWABHIZKLZVRVTAWBGZH
      IZWDWGVSHABMNVQVPWBDEZWHWDOZBAPZVPWIAWBQROWJABSAWBTUAUBUCVQWDWFUDZVPVQWCW
      EUDZWLVQWCHIZWEHIZWCWEVQWCWEUEUFZWNWOUDZVQWCWBUGUFZWBBUEUFZVQWEBUGUFZWPVQ
      WICUHZWBUGUFCUIUJWRWKCWBUKCWBULUMVQWBBUDWSBAUNWBBDUOUPVQUQVQXABUGUFCUIUJW
      TCBUKCBULURWRWSFWCBUEUFBWEUGUFZWPVQWTFWCWBBUSVQWTXBWEBDUTVAWCBWEVBVCVDWCU
      IEZWEUIEZWPWQVEWBVFZBVFZWCWEVGVHVIWBVJBVJVKXCXDWAUIEWMWLVEXEXFAVFWCWEWAVL
      VMVIVNVO $.
      $( [5-Feb-2013] $)
  $}

  ${
    $d B k m n $.  $d B k m x $.
    $( Lemma for ~ domtriom .  This is the equinumerosity version of the
       algebraic identity ` sum_ k e. n ( 2 ^ k ) = ( 2 ^ n ) - 1 ` .
       (Contributed by Mario Carneiro, 7-Feb-2013.) $)
    pwsdompw $p |- ( ( n e. om /\ A. k e. suc n ( B ` k ) ~~ ~P k ) ->
         U_ k e. n ( B ` k ) ~< ( B ` n ) ) $=
      ( vm vx cv com wcel cfv cpw cen wbr csuc wral ciun csdm wi c0 wceq suceq
      raleqdv iuneq1 fveq2 breq12d imbi12d weq 0iun 0ex sucid pweq rcla4v
      ax-mp canth2 pwex ensym cvv wa fvex sdomentr sylancr syl syl5eqbr wss
      sssucid ssralv pm2.27 adantl ccda co visset elelsuc mp2b cdaen syl11anc
      c2o cmap sucex pw2en c1o 2onn elisseti enref ccrd coa 1onn nnacda mpan2
      con0 nnon oa1suc eqtrd cfn wrex nnsdom cdafi sylancl isfinite3 sylibr
      finnum cardid2 3syl eqbrtrrd oprex mapen cxp xpex mapcdaen xp2cda csn
      df1o2 opreq2i mapsnen eqbrtri ensymi xpen mp2an eqbrtrri entr4i entr
      syl2an adantr ancoms cin onfin2 inss2 eqsstri sseli pwfi bitri sylib
      ensdomtr cdom cun iunsuc iunex uncdadom a1i sdomtr sylan2b cardacda
      syl22anc ficardom simpl cardon sylan syl21anc wb cardsdomel2 cardidm
      eleq2i w3a nnaordr biimpa syl31anc nnacl cardnn eleqtrrd cardsdomelir
      elisset domsdomtr expcom sylsyld syld ex com23 finds1 imp ) CFZGHBFZAIZUV
      IJZKLZBUVHMZNZBUVHUVJOZUVHAIZPLZUVNUVQQUVLBRMZNZBRUVJOZRAIZPLZQUVLBDFZMZN
      ZBUWCUVJOZUWCAIZPLZQZUVLBUWDMZNZBUWDUVJOZUWDAIZPLZQCDUVHRSZUVNUVSUVQUWBUW
      OUVLBUVMUVRUVHRTUAUWOUVOUVTUVPUWAPBUVHRUVJUBUVHRAUCUDUECDUFZUVNUWEUVQUWHU
      WPUVLBUVMUWDUVHUWCTUAUWPUVOUWFUVPUWGPBUVHUWCUVJUBUVHUWCAUCUDUEUVHUWDSZUVN
      UWKUVQUWNUWQUVLBUVMUWJUVHUWDTUAUWQUVOUWLUVPUWMPBUVHUWDUVJUBUVHUWDAUCUDUEU
      VSUVTRUWAPBUVJUGUVSUWARJZKLZRUWAPLZRUVRHUVSUWSQRUHUIUVLUWSBRUVRUVIRSUVJUW
      AUVKUWRKUVIRAUCUVIRUJUDUKULUWSRUWRPLZUWRUWAKLZUWTRUHUMUWAUWRRUHUNUOUWAUPH
      UXAUXBUQUWTQRAURRUWRUWAUPUSULUTVAVBUWCGHZUWKUWIUWNUXCUWKUWIUWNQUXCUWKUQZU
      WIUWHUWNUWKUWIUWHQZUXCUWKUWEUXEUWDUWJVCUWKUWEQUWDVDUVLBUWDUWJVEULUWEUWHVF
      VAVGUXDUWGUWGVHVIZUWMKLZUWHUWLUXFPLZUWNUWKUXCUXGUWKUXCUQZUXFUWDJZKLZUXJUW
      MKLZUXGUWKUXFUWCJZUXMVHVIZKLZUXNUXJKLZUXKUXCUWKUWGUXMKLZUXQUXOUWCUWDHUWCU
      WJHUWKUXQQUWCDVJZUIUWCUWDVKUVLUXQBUWCUWJBDUFUVJUWGUVKUXMKUVIUWCAUCZUVIUWC
      UJUDUKVLZUXTUWGUXMUWGUXMUWCAURZUWCUXRUNZUYAUYBVMVNUXCUXJUXNKLZUXPUXCUXJVO
      UWCVPVIZUYDVHVIZKLZUYEUXNKLUYCUXCUXJVOUWDVPVIZKLUYGUYEKLZUYFUWDUWCUXRVQZV
      RUXCUYGVOUWCVSVHVIZVPVIZKLZUYKUYEKLUYHUXCVOVOKLUWDUYJKLUYLVOVOGVTWAZWBUXC
      UYJWCIZUWDUYJKUXCUYNUWCVSWDVIZUWDUXCVSGHZUYNUYOSWEUWCVSWFWGUXCUWCWHHUYOUW
      DSUWCWIUWCWJVAWKUXCUYJWLHZEFZUYJKLEWHWMUYNUYJKLUXCUYJGPLZUYQUXCUWCGPLVSGP
      LZUYSUWCWNUYPUYTWEVSWNULUWCVSWOWPUYJWQWREUYJWSEUYJWTXAXBVOVOUWDUYJUYMUWCV
      SVHXCXDUTUYKUYDVOVSVPVIZXEZUYEUYDVUAVOUWCVPXCZVOVSVPXCZXFVOUWCVSUYMUXRVSG
      WEWAXGUYDVOXEZUYEVUBKUYDVUCXHUYDUYDKLVOVUAKLVUEVUBKLUYDVUCWBVUAVOUYMVUAVO
      RXIZVPVIVOKVSVUFVOVPXJXKVORUHXLXMXNUYDUYDVOVUAVUCVUDXOXPXQXRUYGUYKUYEXSWP
      UXJUYGUYEXSUTUXNUYEUYDUYDVHXCUXMUYDKLZVUGUXNUYEKLUWCUXRVRZVUHUXMUYDUXMUYD
      UYBVUCUYBVUCVMXPXNUXJUYEUXNXSWPUXJUXNUXMUXMVHXCUOVAUXFUXNUXJXSXTUWKUXLUXC
      UWKUWMUXJKLZUXLUWDUWJHUWKVUIQUWDUYIUIUVLVUIBUWDUWJUVIUWDSUVJUWMUVKUXJKUVI
      UWDAUCUVIUWDUJUDUKULUWMUXJUWDUYIUNUOVAYAUXFUXJUWMXSVNYBUXDUWGWLHZUWHUXHQU
      WKUXCVUJUXIUWGGPLZVUJUWKUXQUXMGPLZVUKUXCUXTUXCUWCWLHZVULGWLUWCGWHWLYCWLYD
      WHWLYEYFYGVUMUXMWLHVULUWCYHUXMWQYIYJUWGUXMGYKXTUWGWQZWRYBUWHVUJUXHUWHVUJU
      QZUWLUWFUWGVHVIZYLLVUPUXFPLZUXHUWLUWFUWGYMVUPYLBUWCUVJUWGUXRUXSYNUWFUWGBU
      WCUVJUXRUVIAURYOZUYAYPXMVUOVUPUWGWCIZVUSWDVIZPLZVUTUXFKLZVUQVUOVUPUWFWCIZ
      VUSWDVIZKLZVVDVUTPLZVVAVUOUWFUPHZUWGUPHZUYRUWFKLEWHWMZUYRUWGKLEWHWMZVVEVV
      GVUOVURYQVVHVUOUYAYQVUOUWFWLHZVVIVUOUWFGPLZVVKVUJUWHVUKVVLVUNUWFUWGGYRYSU
      WFWQWRZEUWFWSZVAVUJVVJUWHEUWGWSZVGEUWFUWGYTUUAVUOVVDVUTWCIZHVVFVUOVVDVUTV
      VPVUOVVCGHZVUSGHZVVRVVCVUSHZVVDVUTHZVUOVVKVVQVVMUWFUUBVAVUJVVRUWHUWGUUBZV
      GZVWBVUOVVCVUSPLZVVSVUOVVCUWFKLZUWHUWGVUSKLZVWCVUOVVKVVIVWDVVMVVNEUWFWTXA
      UWHVUJUUCVUJVWEUWHVUJVVJVUSUWGKLVWEVVOEUWGWTVUSUWGUYAUOXAVGVWDUWHUQVVCUWG
      PLZVWEVWCVVCUWFUWGYKVUSWHHZVWFVWEUQVWCQUWGUUDZVVCUWGVUSWHUSULUUEUUFVWCVVC
      VUSWCIZHZVVSVVCWHHVWGVWCVWJUUGUWFUUDVWHVVCVUSUUHXPVWIVUSVVCUWGUUIUUJYIYJV
      VQVVRVVRUUKVVSVVTVVCVUSVUSUULUUMUUNVUJVVPVUTSZUWHVUJVUTGHZVWKVUJVVRVVRVWL
      VWAVWAVUSVUSUUOVNVUTUUPVAVGUUQVVDVUTUURVAVUPVVDVUTYKVNVUJVVBUWHVUJUXFVUTK
      LZVVBVUJVVHVVHVVJVVJVWMUWGWLUUSZVWNVVOVVOEUWGUWGYTUUAUXFVUTVUSVUSWDXCUOVA
      VGUXFUPHVVAVVBUQVUQQUWGUWGVHXCVUPVUTUXFUPUSULVNUWLVUPUXFUUTUTUVAVAUXHUXGU
      WNUWMUPHUXHUXGUQUWNQUWDAURUWLUXFUWMUPUSULUVAUVBUVCUVDUVEUVFUVG $.
      $( [7-Feb-2013] $)
  $}

