$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +c $. $( Cardinal number addition $)

  $( Extend class definition to include cardinal number addition. $)
  ccda $a class +c $.

  ${
    $d x y z $.
    $( Define cardinal number addition.  Definition of cardinal sum in
       [Mendelson] p. 258.  See ~ cdavali for its value and a description. $)
    df-cda $a |- +c = ( x e. _V , y e. _V |->
                   ( ( x X. { (/) } ) u. ( y X. { 1o } ) ) ) $.
  $}

  ${
    $d x y A $.  $d y B $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258. $)
    cdaval $p |- ( ( A e. V /\ B e. W ) -> ( A +c B ) =
                    ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) $=
      ( vx vy wcel cvv ccda csn cxp c1o cun wceq elex wa xpexg mpan2 cv xpeq1
      co c0 p0ex snex anim12i unexb uneq1d uneq2d df-cda ovmpt2g mpd3an3 syl2an
      sylib ) ACGAHGZBHGZABIUAAUBJZKZBLJZKZMZNZBDGACOBDOUNUOUTHGZVAUNUOPUQHGZUS
      HGZPVBUNVCUOVDUNUPHGVCUCAUPHHQRUOURHGVDLUDBURHHQRUEUQUSUFUMEFABHHESZUPKZF
      SZURKZMUTIUQVHMHVEANVFUQVHVEAUPTUGVGBNVHUSUQVGBURTUHEFUIUJUKUL $.
      $( [15-Sep-2013] $) $( [24-Sep-2004] $)
  $}

  ${
    cdaval.1 $e |- A e. _V $.
    cdaval.2 $e |- B e. _V $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258.  For cardinal arithmetic, we follow Mendelson.  Rather than
       defining operations restricted to cardinal numbers, we use this disjoint
       union operation for addition, while cross product and set exponentiation
       stand in for cardinal multiplication and exponentiation.  Equinumerosity
       and dominance serve the roles of equality and ordering.  If we wanted
       to, we could easily convert our theorems to actual cardinal number
       operations via ~ carden , ~ carddom , and ~ cardsdom .  The advantage of
       Mendelson's approach is that we can directly use many equinumerosity
       theorems that we already have available. $)
    cdavali $p |- ( A +c B ) = ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) $=
      ( cvv wcel ccda co c0 csn cxp c1o cun wceq cdaval mp2an ) AEFBEFABGHAIJKB
      LJKMNCDABEEOP $.
      $( [27-Sep-2004] $)

    $( Cardinal addition dominates union. $)
    uncdadom $p |- ( A u. B ) ~<_ ( A +c B ) $=
      ( cun c0 csn cxp c1o ccda co cdom wbr cen xpsnen ensymi endom ax-mp xpex
      wa cin wceq 0ex con0 elexi pm3.2i xp01disj p0ex snex undom mp2an breqtrri
      1on cdavali ) ABEZAFGZHZBIGZHZEZABJKLAUQLMZBUSLMZTUQUSUAFUBUOUTLMVAVBAUQN
      MVAUQACAFCUCOPAUQQRBUSNMVBUSBDBIDIUDUMUEOPBUSQRUFABUGAUQBUSAUPCUHSDBURDIU
      ISUJUKABCDUNUL $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( Cardinal addition is equinumerous to union for disjoint sets. $)
    cdaun $p |- ( ( A i^i B ) = (/) -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( cin c0 wceq ccda co csn cxp c1o cun cen cdavali wbr xp01disj 0ex xpsnen
      wa con0 1on elexi unen mpanl12 mpan syl5eqbr ) ABEFGZABHIAFJKZBLJKZMZABMZ
      NABCDOUIUJEFGZUHUKULNPZABQUIANPUJBNPUMUHTUNAFCRSBLDLUAUBUCSUIAUJBUDUEUFUG
      $.
      $( [5-Apr-2007] $)
  $}

  ${
    cda1en.1 $e |- A e. _V $.
    $( Cardinal addition with cardinal one (which is the same as ordinal one).
       Used in proof of Theorem 6J of [Enderton] p. 143.  (Revised by Mario
       Carneiro, 21-Feb-2013.) $)
    cda1en $p |- ( -. A e. A -> ( A +c 1o ) ~~ suc A ) $=
      ( wcel wn c0 csn cxp c1o cun ccda co csuc cen cin wceq disjsn xp01disj wa
      wbr xpsnen 0ex con0 elexi ensn1 entr4i unen mpanl12 sylbir cdavali df-suc
      1on mpan 3brtr4g ) AACDZAEFGZHHFGZIZAAFZIZAHJKALMUNAURNEOZUQUSMSZAAPUOUPN
      EOZUTVAAHQUOAMSUPURMSVBUTRVAAEBUATUPHURHUBUKUCZHHVCVCTABUDUEUOAUPURUFUGUL
      UHAHBVCUIAUJUM $.
      $( [21-Feb-2013] $) $( [28-Sep-2004] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( Cardinal addition is equinumerous to union for disjoint sets.
       (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaung $p |- ( ( A e. V /\ B e. W /\ ( A i^i B ) = (/) ) ->
                   ( A +c B ) ~~ ( A u. B ) ) $=
      ( vx vy wcel cin c0 wceq ccda co cun cen wbr cv wi eqeq1d breq12d imbi12d
      ineq1 opreq1 uneq1 ineq2 opreq2 uneq2 vex cdaun vtocl2g 3impia ) ACGBDGAB
      HZIJZABKLZABMZNOZEPZFPZHZIJZUPUQKLZUPUQMZNOZQAUQHZIJZAUQKLZAUQMZNOZQULUOQ
      EFABCDUPAJZUSVDVBVGVHURVCIUPAUQUARVHUTVEVAVFNUPAUQKUBUPAUQUCSTUQBJZVDULVG
      UOVIVCUKIUQBAUDRVIVEUMVFUNNUQBAKUEUQBAUFSTUPUQEUGFUGUHUIUJ $.
      $( [5-Jun-2009] $)
  $}

  $( 1+1=2 for cardinal number addition.  Theorem *110.643 of _Principia
     Mathematica_, vol.  II, p. 86, which adds the remark, "The above
     proposition is occasionally useful."  Unlike us, Whitehead and Russell
     define cardinal addition on collections of all sets equinumerous to 1 and
     2 (which for us are proper classes unless we restrict them as in
     ~ karden ), but after applying definitions, our theorem is equivalent.
     See also the comment for ~ pm54.43 .  The comment for ~ cdavali explains
     why we use ` ~~ ` instead of =. $)
  pm110.643 $p |- ( 1o +c 1o ) ~~ 2o $=
    ( c1o ccda co c0 csn cxp cun c2o cen con0 1on cdavali cin wceq wbr xp01disj
    elexi wb 0ex xpsnen pm54.43 mp2an mpbi eqbrtri ) AABCADEFZAAEFZGZHIAAAJKQZU
    HLUEUFMDNZUGHIOZAAPUEAIOUFAIOUIUJRADUHSTAAUHUHTUEUFUAUBUCUD $.
    $( [5-Apr-2007] $)

  ${
    cdaen.1 $e |- A e. _V $.
    cdaen.2 $e |- B e. _V $.
    cdaen.3 $e |- C e. _V $.
    cdaen.4 $e |- D e. _V $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258. $)
    cdaen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( cen wbr wa c0 csn cxp c1o cvv wcel wb xpsnen mp2an cun ccda enen1 enen2
      co 0ex bitri con0 1on elexi cin xp01disj mpanr12 syl2anbr cdavali 3brtr4g
      wceq unen ) ABIJZCDIJZKALMZNZCOMZNZUAZBVANZDVCNZUAZACUBUEBDUBUEIUSVBVFIJZ
      VDVGIJZVEVHIJZUTVIAVFIJZUSAPQVBAIJVIVLREALEUFSVBAVFPUCTBPQVFBIJVLUSRFBLFU
      FSVFBAPUDTUGVJCVGIJZUTCPQVDCIJVJVMRGCOGOUHUIUJZSVDCVGPUCTDPQVGDIJVMUTRHDO
      HVNSVGDCPUDTUGVIVJKVBVDUKLUQVFVGUKLUQVKACULBDULVBVFVDVGURUMUNACEGUOBDFHUO
      UP $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)
  $}

  ${
    $d A w x y z $.  $d B w y z $.  $d C w z $.  $d D w $.  $d W w z $.
    $d X w z $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258.  (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaeng $p |- ( ( ( A e. W /\ B e. X ) /\ ( C e. Y /\ D e. Z ) /\
                     ( A ~~ B /\ C ~~ D ) ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( vz wcel wa cen wbr ccda co wi cv wceq imbi12d vex vw vx vy breq1 anbi2d
      opreq2 breq1d imbi2d breq2 breq2d anbi1d opreq1 vtocl2g impcom 3impia
      cdaen ) AEJBFJKZCGJDHJKZABLMZCDLMZKZACNOZBDNOZLMZURUQVAVDPZUQUSIQZUAQZLMZ
      KZAVFNOZBVGNOZLMZPZPUQUSCVGLMZKZVBVKLMZPZPUQVEPIUACDGHVFCRZVMVQUQVRVIVOVL
      VPVRVHVNUSVFCVGLUDUEVRVJVBVKLVFCANUFUGSUHVGDRZVQVEUQVSVOVAVPVDVSVNUTUSVGD
      CLUIUEVSVKVCVBLVGDBNUFUJSUHUBQZUCQZLMZVHKZVTVFNOZWAVGNOZLMZPAWALMZVHKZVJW
      ELMZPVMUBUCABEFVTARZWCWHWFWIWJWBWGVHVTAWALUDUKWJWDVJWELVTAVFNULUGSWABRZWH
      VIWIVLWKWGUSVHWABALUIUKWKWEVKVJLWABVGNULUJSVTWAVFVGUBTUCTITUATUPUMUMUNUO
      $.
      $( [5-Jun-2009] $)
  $}

  ${
    cda0en.1 $e |- A e. _V $.
    $( Cardinal addition with cardinal zero (the empty set).  Part (a1) of
       proof of Theorem 6J of [Enderton] p. 143. $)
    cda0en $p |- ( A +c (/) ) ~~ A $=
      ( c0 ccda co csn cxp cen c1o cun 0ex cdavali uneq2i 3eqtri xpsnen eqbrtri
      xp0r un0 ) ACDEZACFGZAHSTCIFZGZJTCJTACBKLUBCTUAQMTRNACBKOP $.
      $( [27-Sep-2004] $)

    $( One times a cardinal number. $)
    xp1en $p |- ( A X. 1o ) ~~ A $=
      ( c1o cxp c0 csn cen df1o2 xpeq2i 0ex xpsnen eqbrtri ) ACDAEFZDAGCMAHIAEB
      JKL $.
      $( [27-Sep-2004] $)

    $( Two times a cardinal number.  Exercise 4.56(g) of [Mendelson] p. 258. $)
    xp2cda $p |- ( A X. 2o ) = ( A +c A ) $=
      ( c0 csn c1o cun cxp c2o ccda xpundi cpr df-pr df2o2 df1o2 uneq2i 3eqtr4i
      co sneqi xpeq2i cdavali ) ACDZEDZFZGAUAGAUBGFAHGAAIQAUAUBJHUCACUAKUAUADZF
      HUCCUALMUBUDUAEUANROPSAABBTP $.
      $( [27-Sep-2004] $)
  $}

  ${
    cdacomen.1 $e |- A e. _V $.
    cdacomen.2 $e |- B e. _V $.
    $( Commutative law for cardinal addition.  Exercise 4.56(c) of [Mendelson]
       p. 258. $)
    cdacomen $p |- ( A +c B ) ~~ ( B +c A ) $=
      ( c0 csn cxp c1o cun ccda co cen cin wceq wbr xp01disj 0ex xpsnen cdavali
      entr4i wne 1n0 xpsndisj ax-mp con0 elexi unen mpanl12 mp2an uncom breqtri
      wa 1on 3brtr4i ) AEFZGZBHFZGZIZBUOGZAUQGZIZABJKBAJKLUSVAUTIZVBLUPURMENZVA
      UTMENZUSVCLOZABPHEUAVEUBAHBEUCUDUPVALOURUTLOVDVEULVFUPAVACAECQRAHCHUEUMUF
      ZRTURBUTDBHDVGRBEDQRTUPVAURUTUGUHUIVAUTUJUKABCDSBADCSUN $.
      $( [24-Jul-2006] $) $( [24-Sep-2004] $)

    ${
      cdaassen.3 $e |- C e. _V $.
      $( Associative law for cardinal addition.  Exercise 4.56(c) of
         [Mendelson] p. 258. $)
      cdaassen $p |- ( ( A +c B ) +c C ) ~~ ( A +c ( B +c C ) ) $=
        ( c0 cxp c1o cun ccda co cen wbr cin wceq cvv p0ex xpex xp01disj xpsnen
        csn wcel snex unex xpundir eqeng mp2 1on elexi ensymi wa pm3.2i undisj1
        con0 mpbi mp4an enref xpassen xpcomen mp2an entr4i entri xpeq1i xpindir
        unen xpen 3eqtr3i unass breqtri undisj2 cdavali opreq1i opreq2i 3brtr4i
        xp0r 0ex eqtri ) AGUBZHZBIUBZHZJZVSHZCWAHZJZVTBVSHZWEJZWAHZJZABKLZCKLZA
        BCKLZKLZMWFVTVSHZWBVSHZJZWEWAHZJZWJWDWQMNZWEWRMNWDWEOGPWQWROGPZWFWSMNWD
        QUCWDWQPWTWCVSVTWBAVSDRSZBWAEIUDZSUEZRSVTWBVSUFWDWQQUGUHWRWECWAFXCSZWEI
        XEIUOUIUJUAUKWCCTWOWROGPZWPWROGPZULXAXFXGVTWETZWBWETUMWOWPWRUNUPZWDWQWE
        WRVFUQWSWOWGWAHZWRJZJZWJWOXKVTVSXBRSZXJWRWGWABVSERSZXCSWEWAXEXCSZUEUEWS
        WOXJJZWRJZXLMWQXPMNZWRWRMNXAXPWROGPZWSXQMNWOWOMNWPXJMNWOWPOZGPWOXJOGPZX
        RWOXMURWPBWAVSHZHZXJBWAVSEXCRUSYCBVSWAHZHZXJBYDEVSWARXCSZSBBMNYBYDMNYCY
        EMNBEURWAVSXCRUTBBYBYDEYFVGVABVSWAERXCUSVBVCVTWBOZVSHGVSHXTGYGGVSABTVDV
        TWBVSVEVSVPVHVTWGTZWOWOWPXJVFUQWRXOURXIXFXJWROZGPZULXSXFYJXHWGWEOZWAHGW
        AHYIGYKGWABCTVDWGWEWAVEWAVPVHUMWOXJWRUNUPWQXPWRWRVFUQWOXJWRVIVJVTWOMNWI
        XKMNZVTWIOGPWOXKOGPZWJXLMNWOVTXBVTGXBVQUAUKWIQUCWIXKPYLWHWAWGWEXNXEUEZX
        CSWGWEWAUFWIXKQUGUHAWHTYAXFULYMYAXFYHXHUMWOXJWRVKUPVTWOWIXKVFUQVBVCWLWC
        CKLWFWKWCCKABDEVLVMWCCXDFVLVRWNAWHKLWJWMWHAKBCEFVLVNAWHDYNVLVRVO $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Cardinal multiplication distributes over cardinal addition.  Theorem
         6I(3) of [Enderton] p. 142. $)
      xpcdaen $p |- ( A X. ( B +c C ) ) ~~ ( ( A X. B ) +c ( A X. C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen p0ex xpex wbr cin wceq xpassen 3eqtr3i
        snex unex xp01disj xpeq2i xpindi xp0 unen mp4an xpundi breqtrri cdavali
        ensymi 3brtr4i ) ABGHZIZCJHZIZKZIZABIZUOIZACIZUQIZKZABCLMZIVAVCLMNVEUTA
        USDUPURBUOEOPCUQFJUBZPUCPVEAUPIZAURIZKZUTNVBVHNQVDVINQVBVDRGSVHVIRZGSVE
        VJNQABUODEOTACUQDFVGTVAVCUDAUPURRZIAGIVKGVLGABCUDUEAUPURUFAUGUAVBVHVDVI
        UHUIAUPURUJUKUMVFUSABCEFULUEVAVCABDEPACDFPULUN $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Sum of exponents law for cardinal arithmetic.  Theorem 6I(4) of
         [Enderton] p. 142. $)
      mapcdaen $p |- ( A ^m ( B +c C ) ) ~~ ( ( A ^m B ) X. ( A ^m C ) ) $=
        ( ccda co cmap c0 csn cxp c1o cen wbr xpex xpsnen mapen mp2an oprex cun
        cdavali opreq2i cin wceq xp01disj p0ex snex mapunen ax-mp enref 0ex 1on
        con0 elexi xpen entri eqbrtri ) ABCGHZIHABJKZLZCMKZLZUAZIHZABIHZACIHZLZ
        NUSVDAIBCEFUBUCVEAVAIHZAVCIHZLZVHVAVCUDJUEVEVKNOBCUFVAVCABUTEUGPCVBFMUH
        PDUIUJVIVFNOZVJVGNOZVKVHNOAANOZVABNOVLADUKZBJEULQAAVABDERSVNVCCNOVMVOCM
        FMUNUMUOQAAVCCDFRSVIVFVJVGABITACITUPSUQUR $.
        $( [24-Jul-2006] $) $( [27-Sep-2004] $)

      $( Ordering law for cardinal addition.  Exercise 4.56(f) of [Mendelson]
         p. 258. $)
      cdadom1 $p |- ( A ~<_ B -> ( A +c C ) ~<_ ( B +c C ) ) $=
        ( cdom wbr c0 csn cxp c1o cun ccda co cvv wcel cen wb 0ex xpsnen domen1
        mp2an domen2 bitri snex xpex domrefg ax-mp cin wceq xp01disj p0ex undom
        wa mpan2 sylbir cdavali 3brtr4g ) ABGHZAIJZKZCLJZKZMZBVAKZVDMZACNOBCNOG
        UTVBVFGHZVEVGGHZVHAVFGHZUTAPQVBARHVHVJSDAIDTUAVBAVFPUBUCBPQVFBRHVJUTSEB
        IETUAVFBAPUDUCUEVHVDVDGHZVIVDPQVKCVCFLUFUGZVDPUHUIVHVKUOVFVDUJIUKVIBCUL
        VBVFVDVDBVAEUMUGVLVLUNUPUPUQACDFURBCEFURUS $.
        $( [24-Jul-2006] $) $( [28-Sep-2004] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdadom2 $p |- ( A ~<_ B -> ( C +c A ) ~<_ ( C +c B ) ) $=
        ( cdom wbr ccda co cdadom1 cen cdacomen endomtr mpan domentr mpan2 3syl
        ) ABGHACIJZBCIJZGHZCAIJZTGHZUBCBIJZGHZABCDEFKUBSLHUAUCCAFDMUBSTNOUCTUDL
        HUEBCEFMUBTUDPQR $.
        $( [28-Sep-2004] $)
    $}

    $( A set is dominated by its cardinal sum with another. $)
    cdadom3 $p |- A ~<_ ( A +c B ) $=
      ( cun cdom wbr ccda co cvv wcel wss ssun1 ssdomg mp2 uncdadom domtr mp2an
      ) AABEZFGZSABHIZFGAUAFGAJKASLTCABMASJNOABCDPASUAQR $.
      $( [28-Sep-2004] $)
  $}

  $( The cardinal sum of two finite sets is finite. $)
  cdafi $p |- ( ( A ~< om /\ B ~< om ) -> ( A +c B ) ~< om ) $=
    ( com csdm wbr c0 csn cxp c1o cvv wcel sdomex simpld syl2an wb con0 xpsneng
    cen sylancl sdomen1 wa ccda co cun wceq cdaval 0elon syl2anc ibir 1on unfi2
    eqbrtrd ) ACDEZBCDEZUAABUBUCZAFGHZBIGHZUDZCDUMAJKZBJKZUOURUEUNUMUSCJKZACLMZ
    UNUTVABCLMZABJJUFNUMUPCDEZUQCDEZURCDEUNUMVDUMUSUPAREZVDUMOVBUMUSFPKVFVBUGAF
    JPQSUPACJTUHUIUNVEUNUTUQBREZVEUNOVCUNUTIPKVGVCUJBIJPQSUQBCJTUHUIUPUQUKNUL
    $.
    $( [22-Oct-2004] $)

  ${
    $( Any partition of omega into two pieces (which may be disjoint) contains
       an infinite subset.  (Contributed by Mario Carneiro, 11-Feb-2013.) $)
    cdainflem $p |- ( ( A u. B ) ~~ om -> ( A ~~ om \/ B ~~ om ) ) $=
      ( com cen wbr csdm wa wn wo cdom cvv wcel wss sylancr ssdomg ee10 domentr
      ssexg mpancom anim1i unfi2 sdomnen con2i ianor ssun1 relen brrelexi bren2
      cun syl sylibr ex ssun2 orim12d syl5bi mpd ) ABUIZCDEZACFEZBCFEZGZHZACDEZ
      BCDEZIZVAURVAUQCFEURHABUAUQCUBUJUCVBUSHZUTHZIURVEUSUTUDURVFVCVGVDURVFVCUR
      VFGACJEZVFGVCURVHVFAUQJEZURVHURAKLZAUQMZVIURVKUQKLZVJABUEZUQCDUFUGZAUQKRN
      VMAUQKOPAUQCQSTACUHUKULURVGVDURVGGBCJEZVGGVDURVOVGBUQJEZURVOURBKLZBUQMZVP
      URVRVLVQBAUMZVNBUQKRNVSBUQKOPBUQCQSTBCUHUKULUNUOUP $.
      $( [11-Feb-2013] $)
  $}

  ${
    $d A x y $.
    cdainf.1 $e |- A e. _V $.
    $( A set is infinite iff the cardinal sum with itself is infinite.
       (Revised by Mario Carneiro, 11-Feb-2013.) $)
    cdainf $p |- ( om ~<_ A <-> om ~<_ ( A +c A ) ) $=
      ( vx vy com cdom wbr ccda cv cen wss wa wex domen c0 cin c1o wceq sylancl
      ensym co cdadom3 domtr mpan2 oprex csn cxp cun wo vex cdavali ineq2i indi
      eqtri df-ss biimpi syl5reqr breq1d sylan cdainflem omex inss2 inex1 breq2
      biimpac sseq1 anbi12d cla4ev p0ex xpex sylibr 0ex domentr snex 1onn elexi
      xpsnen jaoi 3syl exlimiv sylbi impbii ) EAFGZEAAHUAZFGZWCAWDFGWEAABBUBEAW
      DUCUDWEECIZJGZWFWDKZLZCMWCCEWDAAHUENWIWCCWIWFAOUFZUGZPZWFAQUFZUGZPZUHZEJG
      ZWLEJGZWOEJGZUIWCWGWFEJGZWHWQEWFCUJZTWHWTWQWHWFWPEJWHWPWFWDPZWFXBWFWKWNUH
      ZPWPWDXCWFAABBUKULWFWKWNUMUNWHXBWFRWFWDUOUPUQURVEUSWLWOUTWRWCWSWREWKFGZWK
      AJGWCWREDIZJGZXEWKKZLZDMZXDWREWLJGZWLWKKZXIWLEVATWFWKVBXHXJXKLDWLWFWKXAVC
      XEWLRXFXJXGXKXEWLEJVDXEWLWKVFVGVHSDEWKAWJBVIVJNVKAOBVLVQEWKAVMSWSEWNFGZWN
      AJGWCWSXFXEWNKZLZDMZXLWSEWOJGZWOWNKZXOWOEVATWFWNVBXNXPXQLDWOWFWNXAVCXEWOR
      XFXPXMXQXEWOEJVDXEWOWNVFVGVHSDEWNAWMBQVNVJNVKAQBQEVOVPVQEWNAVMSVRVSVTWAWB
      $.
      $( [11-Feb-2013] $) $( [22-Oct-2004] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The cardinal and ordinal sums are always equinumerous.  (Contributed by
       Mario Carneiro, 6-Feb-2013.) $)
    onacda $p |- ( ( A e. On /\ B e. On ) -> ( A +o B ) ~~ ( A +c B ) ) $=
      ( vx vy vz con0 wcel wa coa co cv wceq wrex cen wbr wn wi cvv oprex wral
      cab ccda cun cin wex vex eqeq1 rexbidv elab eleq1 biimpd word onelon oacl
      c0 sylan2 eloni ordirr 3syl nsyli imp wss oaword1 sseld adantl exp3acom23
      mtod ex impcom rexlimiva sylbi com13 con2d imnan sylib elin sylnibr nexdv
      necon1bbii abrexexg cdaung syl3an2 mpd3an3 oarec breqtrrd ensym syl simpl
      n0 csn ciun df-iun elsn rexbii abbii eqtri snex a1i rgen iunexg syl5eqelr
      mpan2 simpr enrefg adantr cmpt wf1o wf1 crn wf opreq2 eqid fvmpt ad2antrl
      cfv ad2antll eqeq12d wb ad2ant2lr ad2ant2l oacan syl3anc bitrd ralrimivva
      simpll eqeq2d rcla4ev sylibr jctil dff13 ax-17 cbvmpt rnmpt dff1o5 f1oeng
      fmpt mpbi jctir ensymg sylsyld mpd cdaeng syl222anc entr syl2anc ) AFGZBF
      GZHZABIJZACKZADKZIJZLZDBMZCUAZUBJZNOZUUPABUBJZNOZUUIUURNOUUHUUPUUINOUUQUU
      HUUPAUUOUCZUUINUUFUUGAUUOUDZUOLZUUPUUTNOZUUHEKZUVAGZEUEZPUVBUUHUVEEUUHUVD
      AGZUVDUUOGZHZUVEUUHUVGUVHPQUVIPUUHUVHUVGUUFUUGUVHUVGPZQUVHUUGUUFUVJUVHUVD
      UULLZDBMZUUGUUFUVJQZQZUUNUVLCUVDEUFUUJUVDLUUMUVKDBUUJUVDUULUGUHUIUVKUVNDB
      UVKUUKBGZUVNUVKUUGUVOUVMUVKUUFUUGUVOHZUVJUVKUUFUVPHZUVJUVKUVQHUVGUVDUULGZ
      UVKUVQUVRPUVKUVRUULUULGZUVQUVKUVRUVSUVDUULUULUJUKUVQUULFGZUULULUVSPUVPUUF
      UUKFGZUVTBUUKUMZAUUKUNUPUULUQUULURUSUTVAUVQUVGUVRQUVKUVQAUULUVDUVPUUFUWAA
      UULVBUWBAUUKVCUPVDVEVGVHVFVFVIVJVKVLVAVMUVGUVHVNVOUVDAUUOVPVQVRUVFUVAUOEU
      VAWIVSVOUUGUUFUUORGZUVBUVCDCBUULFVTAUUOFRWAWBWCCDABWDWEUUPUUIABISWFWGUUHU
      UFUUFUWCUUGAANOZUUOBNOZUUSUUFUUGWHZUWFUUHUUODBUULWJZWKZRUWHUUJUWGGZDBMZCU
      AUUODCBUWGWLUWJUUNCUWIUUMDBCUULWMWNWOWPUUGUWHRGZUUFUUGUWGRGZDBTUWKUWLDBUW
      LUVOUULWQWRWSDBUWGFRWTXBVEXAZUUFUUGXCUUFUWDUUGAFXDXEUUHBUUOEBAUVDIJZXFZXG
      ZUWEUUHBUUOUWOXHZUWOXIUUOLZHUWPUUHUWQUWRUUHBUUOUWOXJZUUJUWOXOZUUKUWOXOZLZ
      UUJUUKLZQZDBTCBTZHUWQUUHUXEUWSUUHUXDCDBBUUHUUJBGZUVOHZHZUXBUXCUXHUXBAUUJI
      JZUULLZUXCUXHUWTUXIUXAUULUXFUWTUXILUUHUVOEUUJUWNUXIBUWOUVDUUJAIXKUWOXLZAU
      UJISXMXNUVOUXAUULLUUHUXFEUUKUWNUULBUWOUVDUUKAIXKZUXKAUUKISXMXPXQUXHUUFUUJ
      FGZUWAUXJUXCXRUUFUUGUXGYEUUGUXFUXMUUFUVOBUUJUMXSUUGUVOUWAUUFUXFUWBXTAUUJU
      UKYAYBYCUKYDUWNUUOGZEBTUWSUXNEBUVDBGZUWNUWNLZUXNUWNXLUXOUXPHUWNUULLZDBMZU
      XNUXQUXPDUVDBUUKUVDLUULUWNUWNUUKUVDAIXKYFYGUUNUXRCUWNAUVDISUUJUWNLUUMUXQD
      BUUJUWNUULUGUHUIYHXBWSEBUUOUWNUWOUXKYPYQYICDBUUOUWOYJYHDCBUULUWOEDCBUWNUU
      LUUJUWNGDYKUUJUULGEYKUXLYLYMYRBUUOUWOYNYHUUHUWCUWPBUUONOZUWEUWMUUGUWPUXSQ
      UUFUUGUWPUXSBUUOFUWOYOVHVEBUUORYSYTUUAAAUUOBFFRFUUBUUCUUIUUPUURUUDUUE $.
      $( [6-Feb-2013] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The cardinal sum is equinumerous to an ordinal sum of the cardinals.
       (Contributed by Mario Carneiro, 6-Feb-2013.) $)
    cardacda $p |- ( ( ( A e. _V /\ B e. _V ) /\
       ( E. x e. On x ~~ A /\ E. x e. On x ~~ B ) ) ->
         ( A +c B ) ~~ ( ( card ` A ) +o ( card ` B ) ) ) $=
      ( cvv wcel wa cen wbr con0 wrex ccda ccrd cfv coa cardid2 w3a cardon jctl
      cv co cdaeng syl3an1 syl3an2 onacda mp2an entr oprex ensym 3expia syl2ani
      mpan 3syl imp ) BDEZCDEZFZASZBGHAIJZUQCGHAIJZFBCKTZBLMZCLMZNTZGHZURUPVABG
      HZVBCGHZVDUSABOACOUNUOVEVFFZVDUNUOVGPVAVBKTZUTGHZVCUTGHZVDUOUNVBIEZUOFZVG
      VIUOVKCQZRUNVAIEZUNFVLVGVIUNVNBQZRVABVBCIDIDUAUBUCVCVHGHZVIVJVNVKVPVOVMVA
      VBUDUEVCVHUTUFUKVCUTBCKUGUHULUIUJUM $.
      $( [6-Feb-2013] $)
  $}

  $( The cardinal and ordinal sums of finite ordinals are equal.  (Contributed
     by Paul Chapman, 11-Apr-2009.)  (Revised by Mario Carneiro,
     6-Feb-2013.) $)
  nnacda $p |- ( ( A e. om /\ B e. om ) ->
                 ( card ` ( A +c B ) ) = ( A +o B ) ) $=
    ( com wcel wa coa co ccrd cfv ccda cvv cen wbr wceq con0 nnon onacda syl2an
    oprex carden2b mpsyl nnacl cardnn syl eqtr3d ) ACDZBCDZEZABFGZHIZABJGZHIZUI
    UKKDUHUIUKLMZUJULNABJSUFAODBODUMUGAPBPABQRUIUKKTUAUHUICDUJUINABUBUIUCUDUE
    $.
    $( [6-Feb-2013] $) $( [11-Apr-2009] $)

  ${
    $d A x $.  $d B x $.
    $( The cardinality of the union of disjoint, finite sets is the ordinal sum
       of their cardinalities.  (Contributed by Paul Chapman, 5-Jun-2009.)
       (Revised by Mario Carneiro, 6-Feb-2013.) $)
    ficardun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                   ( card ` ( A u. B ) ) = ( ( card ` A ) +o ( card ` B ) ) )
        $=
      ( vx cfn wcel wceq ccrd cfv co cen wbr ccda con0 wrex cardid2 cvv 3adant3
      wa syl com cin c0 w3a coa cun cv cardon mp2an onacda pm3.2i breq1 rcla4ev
      oacl mp2b fvex simpr wi finnum anim12i cdaeng syl3an3 3expia an4s mpanl12
      mpd cdaung entr syl2anc sylancr wb ficardom nnacda syl2an breq1d carden2b
      mpbid unfi nnacl cardnn eqtr3d ) ADEZBDEZABUAUBFZUCZAGHZBGHZUDIZGHZABUEZG
      HZWGWDWGWIJKZWHWJFZWDWEWFLIZGHZWIJKZWKWDWNWMJKZWMWIJKZWOWGMEZWGWMJKZRCUFZ
      WMJKZCMNWPWRWSWEMEZWFMEZWRAUGZBUGZWEWFUMUHXBXCWSXDXEWEWFUIUHUJXAWSCWGMWTW
      GWMJUKULCWMOUNWDWMABLIZJKZXFWIJKWQWAWBXGWCWEPEZWFPEZWAWBRZXGAGUOBGUOXHXIR
      ZXJRXJXGXKXJUPXHWAXIWBXJXGUQXHWARZXIWBRZXJXGXJXLXMWEAJKZWFBJKZRXGWAXNWBXO
      WAWTAJKCMNXNCAURCAOSWBWTBJKCMNXOCBURCBOSUSWEAWFBPDPDUTVAVBVCVEVDQABDDVFWM
      XFWIVGVHWNWMWIVGVIWAWBWOWKVJWCXJWNWGWIJWAWETEZWFTEZWNWGFWBAVKZBVKZWEWFVLV
      MVNQVPWAWBWKWLUQZWCXJWIDEXTABVQWGWIDVOSQVEWAWBWHWGFZWCWAXPXQYAWBXRXSXPXQR
      WGTEYAWEWFVRWGVSSVMQVT $.
      $( [6-Feb-2013] $) $( [5-Jun-2009] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( The cardinality of the union of finite sets is at most the ordinal sum
       of their cardinalities.  (Contributed by Mario Carneiro, 5-Feb-2013.) $)
    ficardun2 $p |- ( ( A e. Fin /\ B e. Fin ) ->
                   ( card ` ( A u. B ) ) C_ ( ( card ` A ) +o ( card ` B ) ) )
        $=
      ( vx cfn wcel wa cun ccrd cfv coa co wceq wss cdom wbr con0 finnum cardon
      cen wrex cdif undif2 fveq2i cin c0 disjdif ficardun mp3an3 sylan2 syl5eqr
      diffi cv cardid2 3syl difss ssdom2g mpi id syl endomtr ensymg imp domentr
      syl2an syl22anc carddom2 mp2an sylib cardidm 3sstr3g oaword mp3an eqsstrd
      wb adantl ) ADEZBDEZFZABGZHIZAHIZBAUAZHIZJKZWABHIZJKZVRVTAWBGZHIZWDWGVSHA
      BUBUCVQVPWBDEZWHWDLZBAUKZVPWIAWBUDUELWJABUFAWBUGUHUIUJVQWDWFMZVPVQWCWEMZW
      LVQWCHIZWEHIZWCWEVQWCWENOZWNWOMZVQWCWBSOZWBBNOZVQWEBSOZWPVQWICULZWBSOCPTW
      RWKCWBQCWBUMUNVQWBBMWSBAUOWBBDUPUQVQURVQXABSOCPTWTCBQCBUMUSWRWSFWCBNOBWES
      OZWPVQWTFWCWBBUTVQWTXBWEBDVAVBWCBWEVCVDVEWCPEZWEPEZWPWQVNWBRZBRZWCWEVFVGV
      HWBVIBVIVJXCXDWAPEWMWLVNXEXFARWCWEWAVKVLVHVOVM $.
      $( [5-Feb-2013] $)
  $}

  ${
    $d B k m n $.  $d B k m x $.
    $( Lemma for ~ domtriom .  This is the equinumerosity version of the
       algebraic identity ` sum_ k e. n ( 2 ^ k ) = ( 2 ^ n ) - 1 ` .
       (Contributed by Mario Carneiro, 7-Feb-2013.) $)
    pwsdompw $p |- ( ( n e. om /\ A. k e. suc n ( B ` k ) ~~ ~P k ) ->
         U_ k e. n ( B ` k ) ~< ( B ` n ) ) $=
      ( vx com wcel cfv cen wbr csdm wi c0 ax-mp syl ccda co c2o c1o con0 cfn
      vm cpw csuc wral ciun wceq suceq raleqdv iuneq1 fveq2 breq12d imbi12d weq
      cv 0iun 0ex sucid pweq rcla4v canth2 pwex ensym cvv fvex sdomentr sylancr
      syl5eqbr wss sssucid ssralv pm2.27 adantl elelsuc mp2b cdaen syl2anc cmap
      wa vex sucex pw2en 2onn elexi enref ccrd coa 1onn nnacda mpan2 nnon eqtrd
      oa1suc wrex nnsdom cdafi sylancl isfinite3 sylibr finnum cardid2 eqbrtrrd
      3syl oprex mapen cxp mapcdaen xp2cda df1o2 opreq2i mapsnen eqbrtri ensymi
      xpex csn xpen mp2an eqbrtrri entr4i syl2an adantr ancoms cin onfin2 inss2
      entr eqsstri sseli pwfi bitri sylib ensdomtr cdom cun iunsuc a1i cardacda
      syl22anc ficardom cardon expcom iunex uncdadom sdomtr sylan2b simpl sylan
      syl21anc wb cardsdomel2 cardidm eleq2i w3a nnaordr biimpa syl31anc cardnn
      nnacl eleqtrrd cardsdomelir elex domsdomtr sylsyld syld com23 finds1 imp
      ex ) CUNZEFBUNZAGZUVIUBZHIZBUVHUCZUDZBUVHUVJUEZUVHAGZJIZUVNUVQKUVLBLUCZUD
      ZBLUVJUEZLAGZJIZKUVLBUAUNZUCZUDZBUWCUVJUEZUWCAGZJIZKZUVLBUWDUCZUDZBUWDUVJ
      UEZUWDAGZJIZKCUAUVHLUFZUVNUVSUVQUWBUWOUVLBUVMUVRUVHLUGUHUWOUVOUVTUVPUWAJB
      UVHLUVJUIUVHLAUJUKULCUAUMZUVNUWEUVQUWHUWPUVLBUVMUWDUVHUWCUGUHUWPUVOUWFUVP
      UWGJBUVHUWCUVJUIUVHUWCAUJUKULUVHUWDUFZUVNUWKUVQUWNUWQUVLBUVMUWJUVHUWDUGUH
      UWQUVOUWLUVPUWMJBUVHUWDUVJUIUVHUWDAUJUKULUVSUVTLUWAJBUVJUOUVSUWALUBZHIZLU
      WAJIZLUVRFUVSUWSKLUPUQUVLUWSBLUVRUVILUFUVJUWAUVKUWRHUVILAUJUVILURUKUSMUWS
      LUWRJIZUWRUWAHIZUWTLUPUTUWAUWRLUPVAVBUWAVCFUXAUXBVRUWTKLAVDLUWRUWAVCVEMVF
      NVGUWCEFZUWKUWIUWNUXCUWKUWIUWNKUXCUWKVRZUWIUWHUWNUWKUWIUWHKZUXCUWKUWEUXEU
      WDUWJVHUWKUWEKUWDVIUVLBUWDUWJVJMUWEUWHVKNVLUXDUWGUWGOPZUWMHIZUWHUWLUXFJIZ
      UWNUWKUXCUXGUWKUXCVRZUXFUWDUBZHIZUXJUWMHIZUXGUWKUXFUWCUBZUXMOPZHIZUXNUXJH
      IZUXKUXCUWKUWGUXMHIZUXQUXOUWCUWDFUWCUWJFUWKUXQKUWCUAVSZUQUWCUWDVMUVLUXQBU
      WCUWJBUAUMUVJUWGUVKUXMHUVIUWCAUJZUVIUWCURUKUSVNZUXTUWGUXMUWGUXMUWCAVDZUWC
      UXRVAZUYAUYBVOVPUXCUXJUXNHIZUXPUXCUXJQUWCVQPZUYDOPZHIZUYEUXNHIUYCUXCUXJQU
      WDVQPZHIUYGUYEHIZUYFUWDUWCUXRVTZWAUXCUYGQUWCROPZVQPZHIZUYKUYEHIUYHUXCQQHI
      UWDUYJHIUYLQQEWBWCZWDUXCUYJWEGZUWDUYJHUXCUYNUWCRWFPZUWDUXCREFZUYNUYOUFWGU
      WCRWHWIUXCUWCSFUYOUWDUFUWCWJUWCWLNWKUXCUYJTFZDUNZUYJHIDSWMUYNUYJHIUXCUYJE
      JIZUYQUXCUWCEJIREJIZUYSUWCWNUYPUYTWGRWNMUWCRWOWPUYJWQWRDUYJWSDUYJWTXBXAQQ
      UWDUYJUYMUWCROXCXDVFUYKUYDQRVQPZXEZUYEUYDVUAQUWCVQXCZQRVQXCZXMQUWCRUYMUXR
      REWGWCXFUYDQXEZUYEVUBHUYDVUCXGUYDUYDHIQVUAHIVUEVUBHIUYDVUCWDVUAQUYMVUAQLX
      NZVQPQHRVUFQVQXHXIQLUPXJXKXLUYDUYDQVUAVUCVUDXOXPXQXRUYGUYKUYEYEWPUXJUYGUY
      EYEVFUXNUYEUYDUYDOXCUXMUYDHIZVUGUXNUYEHIUWCUXRWAZVUHUXMUYDUXMUYDUYBVUCUYB
      VUCVOXPXLUXJUYEUXNYEWPUXJUXNUXMUXMOXCVBNUXFUXNUXJYEXSUWKUXLUXCUWKUWMUXJHI
      ZUXLUWDUWJFUWKVUIKUWDUYIUQUVLVUIBUWDUWJUVIUWDUFUVJUWMUVKUXJHUVIUWDAUJUVIU
      WDURUKUSMUWMUXJUWDUYIVAVBNXTUXFUXJUWMYEVPYAUXDUWGTFZUWHUXHKUWKUXCVUJUXIUW
      GEJIZVUJUWKUXQUXMEJIZVUKUXCUXTUXCUWCTFZVULETUWCESTYBTYCSTYDYFYGVUMUXMTFVU
      LUWCYHUXMWQYIYJUWGUXMEYKXSUWGWQZWRYAUWHVUJUXHUWHVUJVRZUWLUWFUWGOPZYLIVUPU
      XFJIZUXHUWLUWFUWGYMVUPYLBUWCUVJUWGUXRUXSYNUWFUWGBUWCUVJUXRUVIAVDUUAZUYAUU
      BXKVUOVUPUWGWEGZVUSWFPZJIZVUTUXFHIZVUQVUOVUPUWFWEGZVUSWFPZHIZVVDVUTJIZVVA
      VUOUWFVCFZUWGVCFZUYRUWFHIDSWMZUYRUWGHIDSWMZVVEVVGVUOVURYOVVHVUOUYAYOVUOUW
      FTFZVVIVUOUWFEJIZVVKVUJUWHVUKVVLVUNUWFUWGEUUCUUDUWFWQWRZDUWFWSZNVUJVVJUWH
      DUWGWSZVLDUWFUWGYPYQVUOVVDVUTWEGZFVVFVUOVVDVUTVVPVUOVVCEFZVUSEFZVVRVVCVUS
      FZVVDVUTFZVUOVVKVVQVVMUWFYRNVUJVVRUWHUWGYRZVLZVWBVUOVVCVUSJIZVVSVUOVVCUWF
      HIZUWHUWGVUSHIZVWCVUOVVKVVIVWDVVMVVNDUWFWTXBUWHVUJUUEVUJVWEUWHVUJVVJVUSUW
      GHIVWEVVODUWGWTVUSUWGUYAVBXBVLVWDUWHVRVVCUWGJIZVWEVWCVVCUWFUWGYKVUSSFZVWF
      VWEVRVWCKUWGYSZVVCUWGVUSSVEMUUFUUGVWCVVCVUSWEGZFZVVSVVCSFVWGVWCVWJUUHUWFY
      SVWHVVCVUSUUIXPVWIVUSVVCUWGUUJUUKYIYJVVQVVRVVRUULVVSVVTVVCVUSVUSUUMUUNUUO
      VUJVVPVUTUFZUWHVUJVUTEFZVWKVUJVVRVVRVWLVWAVWAVUSVUSUUQVPVUTUUPNVLUURVVDVU
      TUUSNVUPVVDVUTYKVPVUJVVBUWHVUJUXFVUTHIZVVBVUJVVHVVHVVJVVJVWMUWGTUUTZVWNVV
      OVVODUWGUWGYPYQUXFVUTVUSVUSWFXCVBNVLUXFVCFVVAVVBVRVUQKUWGUWGOXCVUPVUTUXFV
      CVEMVPUWLVUPUXFUVAVFYTNUXHUXGUWNUWMVCFUXHUXGVRUWNKUWDAVDUWLUXFUWMVCVEMYTU
      VBUVCUVGUVDUVEUVF $.
      $( [7-Feb-2013] $)
  $}

