$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rank
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c R1 $. $( Cumulative hierarchy of sets function $)
  $c rank $. $( Rank function $)

  $( Extend class definition to include the cumulative hierarchy of sets
     function. $)
  cr1 $a class R1 $.

  $( Extend class definition to include rank function. $)
  crnk $a class rank $.

  ${
    $d x y z $.
    $( Define the cumulative hierarchy of sets function, using Takeuti and
       Zaring's notation ( ` R1 ` ).  Starting with the empty set, this
       function builds up layers of sets where the next layer is the power
       set of the previous layer (and the union of previous layers when the
       argument is a limit ordinal).  Using the Axiom of Regularity, we can
       show that any set whatsoever belongs to one of the layers of this
       hierarchy (see ~ tz9.13 ).  Our definition expresses Definition 9.9
       of [TakeutiZaring] p. 76 in a closed form, from which we derive the
       recursive definition as theorems ~ r10 , ~ r1suc , and ~ r1lim .
       Theorem ~ r1val1 shows a recursive definition that works for all values,
       and theorems ~ r1val2 and ~ r1val3 show the value expressed in terms of
       rank.  Other notations for this function are  _R_ with the argument
       as a subscript (Equation 3.1 of [BellMachover] p. 477), ` V ` with a
       a subscript (Definition of [Enderton] p. 202), _M_ with a subscript
       (Definition 15.19 of [Monk1] p. 113), the capital Greek letter psi
       (Definition of [Mendelson] p. 281), and bold-face R (Definition 2.1
       of [Kunen] p. 95). $)
    df-r1 $a |- R1 = rec ( { <. x , y >. | y = P~ x } , (/) ) $.

    $( Define the rank function.  See ~ rankval , ~ rankval2 , ~ rankval3 ,
       or ~ rankval4 its value.  The rank is a kind of "inverse" of the
       cumulative hierarchy of sets function ` R1 ` :  given a set, it returns
       an ordinal number telling us the smallest layer of the hierarchy to
       which the set belongs.  Based on Definition 9.14 of [TakeutiZaring]
       p. 79.  Theorem ~ rankid illustrates the "inverse" concept.  Another
       nice theorem showing the relationship is ~ rankr1a . $)
    df-rank $a |- rank =
          { <. x , y >. | y = |^| { z e. On | x e. ( R1 ` suc z ) } } $.
  $}

  ${
    $d x y z w v u $.  $d y z v A $.  $d y v u F $.
    trcl.1 $e |- A e. V $.
    trcl.2 $e |- F =
               ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) $.
    trcl.3 $e |- C = U_ y e. om ( F ` y ) $.
    $( For any set ` A ` , show the properties of its transitive closure
       ` C ` .  Similar to Theorem 9.1 of [TakeutiZaring] p. 73 except that we
       show an explicit expression for the transitive closure rather than just
       its existence.  See ~ tz9.1 for an abbreviated version showing
       existence. $)
    trcl $p |- ( A (_ C /\ Tr C /\ A. x ( ( A (_ x /\ Tr x ) -> C (_ x ) ) $=
      ( vv vu wss wtr cv wa wi wal com cfv ciun wrex c0 wcel peano1 cuni cun
      wceq copab crdg cres fveq1i cvv fr0t ax-mp eqtr2 eqimssi fveq2 sseq2d
      rcla4ev mp2an ssiun sseqtr4 dftr2 csuc eliun anbi2i r19.42v bitr4 ssun2
      fvex uniex unex ax-17 hbopab1 hbrdg hbres eleq2i albii 3imtr4 hbfv hbuni
      hbun unieq uneq12 mpdan frsucopab mpan2 mpbiri sseld elunii syl5 r19.22i
      sylbi peano2 eleq2d ex syl r19.23aiv cbvrexv sylibr ax-gen mpgbir wb
      treq mpbir wral sseq1d eqtr sseq1i biimpr adantr uniss sstr2 df-tr
      syl5ib anc2li unss syl6ib biimprd syl9r com23 adantld finds2 com12
      r19.21aiv cbviunv iunss bitr 3pm3.2i ) EFMFNZEAOZMZYLNZPZFYLMZQZAREBSBOZG
      TZUAZFEYSMZBSUBZEYTMUCSUDEUCGTZMZUUBUEEUUCUUCUCDOCOZUUEUFZUGZUHZCDUIZEUJZ
      SUKZTZEUCGUUKIULZEUMUDUULEUHHEUMUUIUNUOZUPUQUUAUUDBUCSYRUCUHYSUUCEYRUCGUR
      USUTVABSYSEVBUOJVCYKYTNZUUOKOZLOZUDZUUQYTUDZPZUUPYTUDZQZLRKKLYTVDUVBLUUTU
      UPYRVEZGTZUDZBSUBZUVAUUTUURUUQYSUDZPZBSUBZUVFUUTUURUVGBSUBZPUVIUUSUVJUURB
      UUQSYSVFVGUURUVGBSVHVIUVHUVEBSYRSUDZUUPYSUFZUDUVEUVHUVKUVLUVDUUPUVKUVLUVD
      MUVLYSUVLUGZMUVLYSVJUVKUVDUVMUVLUVKUVMUMUDUVDUVMUHYSUVLYRGVKZYSUVNVLVMCDK
      EYRUUGUVMUMGUUPEUDCVNZUUPYRUDCVNZCKYSUVLCKYRGUUPUUKUDZUVQCRUUPGUDZUVRCRCK
      UUJSCKEUUIUUHCDKVOUVOVPUUPSUDZCVNVQGUUKUUPIVRZUVRUVQCUVTVSVTUVPWAZCKYSUWA
      WBWCIUUEYSUHUUFUVLUHUUGUVMUHUUEYSWDUUEYSUUFUVLWEWFWGWHZUSWIWJUUPUUQYSWKWL
      WMWNUVFUUPYSUDZBSUBZUVAUVFUUPUUQGTZUDZLSUBZUWDUVEUWGBSUVKUVCSUDZUVEUWGQYR
      WOUWHUVEUWGUWFUVELUVCSUUQUVCUHUWEUVDUUPUUQUVCGURWPUTWQWRWSUWCUWFBLSYRUUQU
      HYSUWEUUPYRUUQGURWPWTXABUUPSYSVFXAWRXBXCFYTUHYKUUOXDJFYTXEUOXFYQAYOUUPGTZ
      YLMZKSXGZYPYOUWJKSUVSYOUWJUWJUUCYLMZYSYLMZUVDYLMZYOKBUUPUCUHUWIUUCYLUUPUC
      GURXHUUPYRUHUWIYSYLUUPYRGURXHUUPUVCUHUWIUVDYLUUPUVCGURXHYMUWLYNUWLYMUUCEY
      LUUCUULEUUMUUNXIXJXKXLUVKYNUWMUWNQYMUVKUWMYNUWNUWMYNUVMYLMZUVKUWNUWMYNUWM
      UVLYLMZPUWOUWMYNUWPUWMUVLYLUFZMZYNUWPQYSYLXMUWRUWQYLMUWPYNUVLUWQYLXNYLXOX
      PWRXQYSUVLYLXRXSUVKUWNUWOUVKUVDUVMYLUWBXHXTYAYBYCYDYEYFYPKSUWIUAZYLMUWKFU
      WSYLFYTUWSJBKSYSUWIYRUUPGURYGXIXJKSUWIYLYHYIXAXBYJ $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d A x y z w v $.
    tz9.1.1 $e |- A e. V $.
    $( Every set has a transitive closure (smallest transitive extension).
       Theorem 9.1 of [TakeutiZaring] p. 73.  See ~ trcl for an explicit
       expression for the transitive closure. $)
    tz9.1 $p |- E. x ( A (_ x /\ Tr x
                /\ A. y ( ( A (_ y /\ Tr y ) -> x (_ y ) ) $=
      ( vz vv vw com cv cuni cun wceq copab crdg cres cfv ciun wss wtr wa wi
      wal w3a wex eqid trcl omex fvex iunex sseq2 treq sseq1 imbi2d albidv
      3anbi123d cla4ev ax-mp ) CEHEIZFIGIZUSJKLGFMCNHOZPZQZRZVBSZCBIZRVESTZVBVE
      RZUAZBUBZUCZCAIZRZVKSZVFVKVERZUAZBUBZUCZAUDBEGFCVBUTDUTUEVBUEUFVQVJAVBEHV
      AUGURUTUHUIVKVBLZVLVCVMVDVPVIVKVBCUJVKVBUKVRVOVHBVRVNVGVFVKVBVEULUMUNUOUP
      UQ $.
      $( [15-Sep-2003] $)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity, which does not require
       that ` A ` be a set.  Axiom 6' of [TakeutiZaring] p. 21.  The proof
       makes use of a special case of Proposition 9.4 of [TakeutiZaring]
       p. 75. $)
    zfregs $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vz vy vw c0 wne cv wcel wex cin wceq wrex ne0 csn wss wtr wa wi wal
      w3a snex tz9.1 trel inass incom ineq2i eqtr eleq2i elin bitr2 ne0i sylbi
      ex syl6 exp3a com34 imp3a inss1 sseli ancri syl5 19.23adv syl5ib com23
      imp necon4d anim2d anbi1i anass bitr r19.22dv2 visset inex1 zfreg2 snssi
      anim2i ssin snss bitr4 sylib syl impcom 3adant3 19.23aiv ax-mp ) BFGCHZBI
      ZCJAHZBKZFLZABMZCBNWHWLCWGOZDHZPZWNQZWMEHZPWQQRWNWQPSETZUAZDJWHWLSZDEWMWG
      UBUCWSWTDWOWPWTWRWPWOWTWPWOWHWLWPWNBKZFGZWLWOWHRZWPXAWIKZFLZAXAMWLXBWPXEW
      KAXABWPWIWNIZWIBIZXERZRZXGWKRZWIXAIZXERZWPXFXHXJWPXFXHXJSWPXFRZXEWKXGXMWJ
      FXDFWPXFWJFGZXDFGZSWPXNXFXOWPWQWJIZEJXFXOSZXNWPXPXQEWPWQWIIZXPRXQXPWPXRXP
      XQWPXRXFXPXOWPXRXFXPXOSZWPXRXFRWQWNIZXSWNWQWIUDXTXPXOXTXPRZWQXDIZXOYBWQWN
      WJKZIYAXDYCWQXDWNBWIKZKYCWNBWIUEYDWJWNBWIUFUGUHUIWQWNWJUJUKXDWQULUMUNUOUP
      UQURXPXRWJWIWQWIBUSUTVAVBVCEWJNVDVEVFVGVHUNURXLXFXGRZXERXIXKYEXEWIWNBUJVI
      XFXGXEVJVKVDVLAXAWNBDVMVNVOVBXCWGXAIZXBXCWOWMBPZRZYFWHYGWOWGBVPVQYHWMXAPY
      FWMWNBVRWGXACVMVSVTWAXAWGULWBVBUPWCWDWEWFWEUM $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y z A $.
    $( Set (epsilon) induction.  Theorem 5.22 of [TakeutiZaring] p. 21. $)
    setind $p |- ( A. x ( x (_ A -> x e. A ) -> A = V ) $=
      ( vy cv wss wcel wi wal cvv cdif c0 wceq cin wrex wn sseq1 eleq1 imbi12d
      a4b1 ssindif0 syl5ibr eldifn nsyli imp nrexdv zfregs necon1bi syl vdif0
      sylibr ) ADZBEZUKBFZGZAHZIBJZKLZBILUOCDZUPMKLZCUPNZOUQUOUSCUPUOURUPFZUSOU
      OUSURBFZVAUOURBEZVBUSUNVCVBGACUKURLULVCUMVBUKURBPUKURBQRSURBTUAURIBUBUCUD
      UEUTUPKCUPUFUGUHBUIUJ $.
      $( [17-Sep-2003] $)
  $}

  ${
    $d x A $.
    $( Set (epsilon) induction, stated compactly.  Given as a homework problem
       in 1992 by George Boolos (1940-1996). $)
    setind2 $p |- ( P~ A (_ A -> A = V ) $=
      ( vx cpw wss cv wcel wi wal cvv wceq dfss2 visset elpw imbi1i albii bitr
      setind sylbi ) ACZADZBEZADZUAAFZGZBHZAIJTUASFZUCGZBHUEBSAKUGUDBUFUBUCUAAB
      LMNOPBAQR $.
      $( [17-Sep-2003] $)
  $}

  ${
    $d x y $.
    $( The cumulative hierarchy of sets function is a function on the class of
       ordinal numbers. $)
    r1fnon $p |- R1 Fn On $=
      ( vy vx cr1 con0 wfn cv cpw wceq copab c0 crdg rdgfnon wb df-r1 fneq1
      ax-mp mpbir ) CDEZAFBFGHBAIZJKZDEZJSLCTHRUAMBANDCTOPQ $.
      $( [5-Oct-2003] $) $( [5-Oct-2003] $)

    $( Value of the cumulative hierarchy of sets function at ` (/) ` .
       Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r10 $p |- ( R1 ` (/) ) = (/) $=
      ( vy vx c0 cr1 cfv cv cpw wceq copab crdg df-r1 fveq1i 0ex rdg0 eqtr ) CD
      ECAFBFGHBAIZCJZECCDQBAKLCPMNO $.
      $( [2-Sep-2003] $)
  $}

  ${
    $d x y z A $.
    $( Value of the cumulative hierarchy of sets function at a successor
       ordinal.  Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r1suc $p |- ( A e. On -> ( R1 ` suc A ) = P~ ( R1 ` A ) ) $=
      ( vx vy vz con0 wcel cr1 cfv cpw cvv csuc wceq fvex pwex c0 cv ax-17
      df-r1 pweq rdgsucopab mpan2 ) AEFAGHZIZJFAKGHUCLUBAGMNBCDOABPZIUCJGDPZOFB
      QUEAFBQUEUCFBQBCRUDUBSTUA $.
      $( [2-Sep-2003] $)
  $}

  ${
    $d x y z A $.
    $( Value of the cumulative hierarchy of sets function at a limit
       ordinal.  Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r1lim $p |- ( ( A e. B /\ Lim A ) -> ( R1 ` A ) = U_ x e. A ( R1 ` x ) ) $=
      ( vz vy wcel wlim wa cv cpw wceq copab c0 crdg cfv ciun cr1 rdglim2a
      df-r1 fveq1i a1i iuneq2i 3eqtr4g ) BCFBGHBDIEIJKEDLZMNZOABAIZUEOZPBQOABUF
      QOZPAMBCUDRBQUEEDSZTABUHUGUHUGKUFBFUFQUEUITUAUBUC $.
      $( [4-Oct-2003] $) $( [4-Oct-2003] $)
  $}

  ${
    $d x y z A $.
    $( The cumulative hierarchy of sets is transitive.  Lemma 7T of [Enderton]
       p. 202. $)
    r1tr $p |- Tr ( R1 ` A ) $=
      ( vx vy vz con0 wcel cr1 cfv wtr cv c0 csuc wceq wb fveq2 treq syl tr0
      r10 ax-mp mpbir wa wss wral cpw r1suc eleq2d visset elpw syl6bb adantr
      ssel wi dftr4 sylbi biimprd sylan9r ssrdv ex sylbid r19.21aiv dftr3
      sylibr wlim wrex ciun cvv r1lim mpan eliun biimp syl6bi hbra1 ra4 trss
      syl6 r19.22d sylan9 sseq2d ssiun syl5bir syld tfinds wn cdm wfn r1fnon
      fndm eleq2i negbii ndmfv sylbir mpbiri pm2.61i ) AEFZAGHZIZBJZGHZIZKGHZIZ
      CJZGHZIZXCLZGHZIZWQBCAWRKMWSXAMWTXBNWRKGOWSXAPQWRXCMWSXDMWTXENWRXCGOWSXDP
      QWRXFMWSXGMWTXHNWRXFGOWSXGPQWRAMWSWPMWTWQNWRAGOWSWPPQXBKIZRXAKMXBXINSXAKP
      TUAXCEFZXEXHXJXEUBZWRXGUCZBXGUDXHXKXLBXGXKWRXGFZWRXDUCZXLXJXMXNNXEXJXMWRX
      DUEZFXNXJXGXOWRXCUFZUGWRXDBUHZUIUJUKXKXNXLXKXNUBDWRXGXNDJZWRFXRXDFZXKXRXG
      FZWRXDXRULXEXSXRXOFZXJXTXEXDXOUCXSYAUMXDUNXDXOXRULUOXJXTYAXJXGXOXRXPUGUPU
      QUQURUSUTVABXGVBVCUSWRVDZXECWRUDZWTYBYCUBZXRWSUCZDWSUDWTYDYEDWSYDXRWSFZXR
      XDUCZCWRVEZYEYBYFXSCWRVEZYCYHYBYFXRCWRXDVFZFZYIYBWSYJXRWRVGFYBWSYJMXQCWRV
      GVHVIZUGYKYICXRWRXDVJVKVLYCXSYGCWRXECWRVMYCXCWRFXEXSYGUMXECWRVNXDXRVOVPVQ
      VRYBYHYEUMYCYBYEXRYJUCYHYBWSYJXRYLVSCWRXDXRVTWAUKWBVADWSVBVCUSWCWOWDZWQXI
      RYMWPKMZWQXINYMAGWEZFZWDYNYPWOYOEAGEWFYOEMWGEGWHTWIWJAGWKWLWPKPQWMWN $.
      $( [26-Nov-2003] $) $( [8-Sep-2003] $)
  $}

  ${
    $d x y A $. $d x y B $.
    $( Ordering relation for the cumulative hierarchy of sets.  Part of
       Proposition 9.10(2) of [TakeutiZaring] p. 77. $)
    r1ord $p |- ( B e. On -> ( A e. B -> ( R1 ` A ) e. ( R1 ` B ) ) ) $=
      ( vx vy con0 wcel cr1 cfv wi csuc wa wss cv wceq eleq2 fveq2 eleq2d
      imbi12d onelon cpw r1suc fvex pwid syl5eleqr syl ex wtr r1tr trss ax-mp
      sseld imim2d cvv elisset sucexb sylibr sucssel imp syl7 a1d com24 exp3a
      imp31 wlim wral ciun wrex rcla4ev limsuc biimpa word limord visset elon
      sylan sylanc eliun wb r1lim mpan adantr mpbird ad2antrr tfindsg pm3.26
      suceloni jca eloni ordsucss pm2.43d ) BEFZABFZAGHZBGHZFZWKWLWLWOIZWKAJZEF
      ZKWQBLZWPWKWLKZACMZFZWMXAGHZFZIZAWQFZWMWQGHZFZIADMZFZWMXIGHZFZIZAXIJZFZWM
      XNGHZFZIZWPCDBWQXAWQNZXBXFXDXHXAWQAOXSXCXGWMXAWQGPQRXAXINZXBXJXDXLXAXIAOX
      TXCXKWMXAXIGPQRXAXNNZXBXOXDXQXAXNAOYAXCXPWMXAXNGPQRXABNZXBWLXDWOXABAOYBXC
      WNWMXABGPQRWRXFXHWRXFKAEFZXHWQASYCWMTXGWMAUAWMAGUBUCUDZUEUFXIEFZWRWQXILZX
      MXRIZYEWRYFYGYEXOXMWRYFKZXQYEXMYHXQIIXOYEXMXJXQYHYEXLXQXJYEXKXPWMYEXKXPFZ
      XKXPLZYEXKTXPXKXIUAXKXIGUBUCUDXPUGYIYJIXNUHXPXKUIUJUEUKULWRYFXJWRAUMFZYFX
      JIWRWQUMFYKWQEUNAUOUPAXIUMUQUEURUSUTVAVBVCXAVDZWRKWQXALZKXEYFXMIDXAVEYLXE
      WRYMYLXBXDYLXBKZXDWMDXAXKVFZFZYNXLDXAVGZYPWQXAFZXHYQYNXLXHDWQXAXIWQNXKXGW
      MXIWQGPQVHYLXBYRXAAVIVJYNYCXHXAEFZXBYCYLXAASYLXAVKYSXAVLXACVMZVNUPVOYDUEV
      PDWMXAXKVQUPYLXDYPVRXBYLXCYOWMXAUMFYLXCYONYTDXAUMVSVTQWAWBUFWCUTWDWTWKWRW
      KWLWEWTYCWRBASAWFUEWGWKWLWSWKBVKWLWSIBWHABWIUEURVPUFWJ $.
      $( [8-Sep-2003] $)
  $}

  $( Ordering relation for the cumulative hierarchy of sets.  Part of
     Proposition 9.10(2) of [TakeutiZaring] p. 77. $)
  r1ord2 $p |- ( B e. On -> ( A e. B -> ( R1 ` A ) (_ ( R1 ` B ) ) ) $=
    ( con0 wcel cr1 cfv wss r1ord wtr wi r1tr trss ax-mp syl6 ) BCDABDAEFZBEFZD
    ZOPGZABHPIQRJBKPOLMN $.
    $( [22-Sep-2003] $)

  $( Ordering relation for the cumulative hierarchy of sets.  Part of
     Theorem 3.3(i) of [BellMachover] p. 478. $)
  r1ord3 $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B ->
               ( R1 ` A ) (_ ( R1 ` B ) ) ) $=
    ( con0 wcel wa wss wceq wo cr1 cfv onsseleq wi r1ord2 adantl fveq2 eqimss
    syl a1i jaod sylbid ) ACDZBCDZEZABFABDZABGZHAIJZBIJZFZABKUCUDUHUEUBUDUHLUAA
    BMNUEUHLUCUEUFUGGUHABIOUFUGPQRST $.
    $( [22-Sep-2003] $)

  ${
    $d x y z w v A $.
    $( The value of the cumulative hierarchy of sets function expressed
       recursively.  Theorem 7Q of [Enderton] p. 202. $)
    r1val1 $p |- ( A e. On -> ( R1 ` A ) = U_ x e. A P~ ( R1 ` x ) ) $=
      ( vy con0 wcel cr1 cfv cv cpw ciun c0 wceq csuc wrex cvv wlim wa w3o wss
      onzsl 0ss fveq2 r10 syl6eq sseq1d mpbiri ax-17 hbiu1 hbss r1suc
      sylan9eqr visset sucid eleq2 ssiun2 syl adantl eqsstrd ex r19.23ai r1lim
      wral word ordelon limord sylan sucelon r1ord2 mpi sylbi sseqtrd
      r19.21aiva ss2iun 3jaoi onelon r1ord3 sylib pm3.26 jca wi eloni ordsucss
      imp sylc eqsstr3d iunss sylibr eqssd ) BDEZBFGZABAHZFGZIZJZWIBKLZBWKMZLZA
      DNZBOEZBPZQZRWJWNSZABTWOXBWRXAWOXBKWNSWNUAWOWJKWNWOWJKFGKBKFUBUCUDUEUFWQX
      BADACWJWNCHWJEAUGACBWMUHUIWKDEZWQXBXCWQQWJWMWNWQXCWJWPFGZWMBWPFUBWKUJZUKW
      QWMWNSZXCWQWKBEZXFWQXGWKWPEZWKAULUMZBWPWKUNUFABWMUOUPUQURUSUTXAWJABWLJZWN
      ABOVAWTXJWNSZWSWTWLWMSZABVBXKWTXLABWTXGQXCXLBVCZXGXCWTBWKVDBVEVFXCWLXDWMX
      CWPDEZWLXDSZWKVGZXNXHXOXIWKWPVHVIVJXEVKUPVLABWLWMVMUPUQURVNVJWIWMWJSZABVB
      WNWJSWIXQABWIXGQZWMXDWJXRXCXDWMLBWKVOZXEUPXNWIQWPBSZXDWJSXRWPBVPXRXNWIXRX
      CXNXSXPVQWIXGVRVSWIXGXTWIXMXGXTVTBWAWKBWBUPWCWDWEVLABWMWJWFWGWH $.
      $( [28-Feb-2004] $) $( [25-Nov-2003] $)
  $}

  ${
    $d x y z w v A $.  $d x y u F $.
    tz9.12lem.1 $e |- A e. V $.
    tz9.12lem.2 $e |- F =
                   { <. z , w >. | w = |^| { v e. On | z e. ( R1 ` v ) } } $.
    $( Lemma for ~ tz9.12 . $)
    tz9.12lem1 $p |- ( F " A ) (_ On $=
      ( vy vx cima con0 cv wcel cop wa wex visset elima3 cr1 cfv crab cint
      wceq copab eleq2i eleq1 rabbisdv inteqd eqeq2d eqeq1 opelopab bitr cvv
      19.8a isset sylibr c0 wne wi intex wss ssrab2 oninton mpan syl5bir com12
      sylbir mpcom sylbi adantl 19.23aiv ssriv ) HEDJZKHLZVMMILZDMZVOVNNZEMZOZI
      PVNKMZIVNEDHQZRVSVTIVRVTVPVRVNVOCLSTZMZCKUAZUBZUCZVTVRVQBLZALZWBMZCKUAZUB
      ZUCZABUDZMWFEWMVQGUEWLWGWEUCWFABVOVNIQWAWHVOUCZWKWEWGWNWJWDWNWIWCCKWHVOWB
      UFUGUHUIWGVNWEUJUKULWEUMMZWFVTWFWFHPWOWFHUNHWEUOUPWOWDUQURZWFVTUSWDUTWFWP
      VTWFVTWEKMZWPVNWEKUFWDKVAWPWQWCCKVBWDVCVDVEVFVGVHVIVJVKVIVL $.
      $( [22-Sep-2003] $)

    $( Lemma for ~ tz9.12 . $)
    tz9.12lem2 $p |- suc U. ( F " A ) e. On $=
      ( cima cuni con0 wss wcel tz9.12lem1 wfun cvv cv cr1 cfv crab cint wceq
      copab funopabeq wb funeq ax-mp mpbir funimaex ssonuni onsuc ) EDHZIZUKJKU
      LJLABCDEFGMUKENZUKOLUMBPAPCPQRLCJSTZUAABUBZNZABUNUCEUOUAUMUPUDGEUOUEUFUGE
      DFUHUFUIUFUJ $.
      $( [22-Sep-2003] $)

    $( Lemma for ~ tz9.12 . $)
    tz9.12lem3 $p |- ( A. x e. A E. y e. On x e. ( R1 ` y ) ->
                    A e. ( R1 ` suc suc U. ( F " A ) ) ) $=
      ( cv cr1 cfv wcel con0 wrex wral cima cuni csuc wa wss cdm wi crab c0
      wne wceq fveq2 eleq2d rcla4ev rabn0 sylibr cint cvv cop wex copab eleq2i
      visset eleq1 rabbisdv inteqd eqeq2d eqeq1 opelopab bitr exbii eldm2
      isset 3bitr4 intex bitr4 wfun funopabeq wb funeq ax-mp mpbir funfvima
      mpan syl tz9.12lem1 onsucuni sseli tz9.12lem2 r1ord2 syl6 imp fvopabg
      fveq1i syl5eq sylbi ssrab2 onint eqeltrd wal hbrab1 hbint hbeleq hbopab
      albii 3imtr4 ax-17 hbfv hbel elrabf pm3.27bd 3syl adantr sseldd exp31
      com3r r19.23adv r19.20i cpw r1suc elpw dfss3 3bitr ) AJZBJZKLZMZBNOZAFPXT
      GFQZRSZKLZMZAFPZFYFSKLZMZYDYHAFXTFMZYCYHBNYANMZYCYLYHYMYCYLYHYMYCTZYLTXTG
      LZKLZYGXTYNYLYPYGUAZYNYLYOYEMZYQYNXTGUBMZYLYRUCZYNXTEJZKLZMZENUDZUEUFZYSY
      NUUCENOUUEUUCYCEYANUUAYAUGUUBYBXTUUAYAKUHUIUJUUCENUKULZYSUUDUMZUNMZUUEXTY
      AUOZGMZBUPYAUUGUGZBUPYSUUHUUJUUKBUUJUUIDJZCJZUUBMZENUDZUMZUGZCDUQZMUUKGUU
      RUUIIURUUQUULUUGUGUUKCDXTYAAUSZBUSUUMXTUGZUUPUUGUULUUTUUOUUDUUTUUNUUCENUU
      MXTUUBUTVAVBZVCUULYAUUGVDVEVFVGBXTGUUSVHBUUGVIVJUUDVKZVLULGVMZYSYTUVCUURV
      MZCDUUPVNGUURUGUVCUVDVOIGUURVPVQVRFXTGVSVTWAYRYOYFMZYQYEYFYOYENUAYEYFUACD
      EFGHIWBYEWCVQWDYFNMZUVEYQUCCDEFGHIWEZYOYFWFVQWAWGWHYNXTYPMZYLYNUUEYOUUDMZ
      UVHUUFUUEYOUUGUUDUUEUUHYOUUGUGUVBUUHXTUURLZUUGYOXTUNMUUHUVJUUGUGUUSCDXTUU
      PUUGUNUNUVAWIVTXTGUURIWJWKWLUUDNUAUUEUUGUUDMUUCENWMUUDWNVTWOUVIYONMUVHUUC
      UVHEBYONEBXTGYAUURMZUVKEWPYAGMZUVLEWPUUQCDEBEDUUPEDUUOUUNEDNWQWRWSWTGUURY
      AIURZUVLUVKEUVMXAXBYAXTMEXCZXDZYMEXCEBBXTYPUVNEBYOKYAKMEXCUVOXDXEUUAYOUGU
      UBYPXTUUAYOKUHUIXFXGXHXIXJXKXLXMXNYKFYGXOZMFYGUAYIYJUVPFUVFYJUVPUGUVGYFXP
      VQURFYGHXQAFYGXRXSUL $.
      $( [22-Sep-2003] $)
  $}

  ${
    $d x y z w v A $.
    tz9.12.1 $e |- A e. V $.
    $( A set is well-founded if all of its elements are well-founded.
       Proposition 9.12 of [TakeutiZaring] p. 78.  The main proof consists
       of ~ tz9.12lem1 through ~ tz9.12lem3 . $)
    tz9.12 $p |- ( A. x e. A E. y e. On x e. ( R1 ` y ) ->
              E. y e. On A e. ( R1 ` y ) ) $=
      ( vw vz vv cv cr1 cfv wcel con0 wrex wral crab cint wceq copab cima cuni
      csuc eqid tz9.12lem3 tz9.12lem2 onsuc fveq2 eleq2d rcla4ev mpan syl ) AHB
      HZIJZKBLMACNCEHFHGHIJKGLOPQFERZCSTUAZUAZIJZKZCULKZBLMZABFEGCUMDUMUBZUCUOL
      KUQUSUNFEGCUMDUTUDUEURUQBUOLUKUOQULUPCUKUOIUFUGUHUIUJ $.
      $( [22-Sep-2003] $)
  $}

  ${
    $d x y z w A $.
    tz9.13.1 $e |- A e. V $.
    $( Every set is well-founded, assuming the Axiom of Regularity.  In other
       words, every set belongs to a layer of the cumulative hierarchy of sets.
       Proposition 9.13 of [TakeutiZaring] p. 78. $)
    tz9.13 $p |- E. x e. On A e. ( R1 ` x ) $=
      ( vy vz vw cv cr1 cfv wcel con0 wrex cab cvv wss wi wceq setind wral
      ssel visset eleq1 rexbidv elab syl6ib r19.21aiv tz9.12 syl sylibr mpg
      eleqtrr mpbi ) BDGZAGHIZJZAKLZDMZJBUNJZAKLZBNUQCEGZUQOZUTUQJZPUQNQEEUQRVA
      UTUNJZAKLZVBVAFGZUNJZAKLZFUTSVDVAVGFUTVAVEUTJVEUQJVGUTUQVETUPVGDVEFUAUMVE
      QUOVFAKUMVEUNUBUCUDUEUFFAUTEUAZUGUHUPVDDUTVHUMUTQUOVCAKUMUTUNUBUCUDUIUJUK
      UPUSDBCUMBQUOURAKUMBUNUBUCUDUL $.
      $( [23-Sep-2003] $)
  $}

  ${
    $d x y A $.
    $( Every set is well-founded, assuming the Axiom of Regularity.
       Proposition 9.13 of [TakeutiZaring] p. 78.  This variant of ~ tz9.13
       expresses the class existence requirement as an antecedent. $)
    tz9.13g $p |- ( A e. B -> E. x e. On A e. ( R1 ` x ) ) $=
      ( vy cv cr1 cfv wcel con0 wrex wceq ax-17 eleq1 rexbid visset tz9.13
      vtoclg ) DEZAEFGZHZAIJBSHZAIJDBCRBKZTUAAIUBALRBSMNARDOPQ $.
      $( [4-Oct-2003] $) $( [4-Oct-2003] $)
  $}

  ${
    $d x A $.
    $( Every set is well-founded, assuming the Axiom of Regularity.
       Proposition 9.13 of [TakeutiZaring] p. 78.   This variant of
       ~ tz9.13g is useful in proofs of theorems about the rank function. $)
    rankwflem $p |- ( A e. B -> E. x e. On A e. ( R1 ` suc x ) ) $=
      ( wcel cv cr1 cfv con0 wrex csuc tz9.13g wss suceloni visset sucid
      r1ord2 mpi syl sseld r19.22i ) BCDBAEZFGZDZAHIBUAJZFGZDZAHIABCKUCUFAHUAHD
      ZUBUEBUGUDHDZUBUELZUAMUHUAUDDUIUAANOUAUDPQRSTR $.
      $( [4-Oct-2003] $) $( [4-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( Every set belongs to some value of the cumulative hierarchy of sets
       function ` R1 ` , i.e. the indexed union of all values of ` R1 ` is the
       universe.  Lemma 9.3 of [Jech] p. 71. $)
    jech9.3 $p |- U_ x e. On ( R1 ` x ) = V $=
      ( vy con0 cv cr1 cfv ciun cvv wcel wrex visset tz9.13 eliun mpbir 2th
      eqriv ) BACADEFZGZHBDZRIZSHITSQIACJASBKZLASCQMNUAOP $.
      $( [12-Oct-2003] $) $( [4-Oct-2003] $)

    $( The cumulative hierarchy of sets covers the universe.  Proposition 4.45
       (b) to (a) of [Mendelson] p. 281. $)
    unir1 $p |- U. ( R1 " On ) = V $=
      ( vx cr1 con0 cima cuni crn cv cfv ciun cvv cdm wceq wfn r1fnon fndm
      ax-mp imaeq2 imadmrn eqtr3 unieqi fniunfv jech9.3 3eqtr2 ) BCDZEBFZEZACAG
      BHIZJUDUEBBKZDZUDUEUHCLZUIUDLBCMZUJNCBOPUHCBQPBRSTUKUGUFLNACBUAPAUBUC $.
      $( [16-Nov-2004] $) $( [27-Sep-2004] $)
  $}

  ${
    $d x y z A $.
    rankval.1 $e |- A e. V $.
    $( Value of the rank function.  Definition 9.14 of [TakeutiZaring]
       p. 79 (proved as a theorem from our definition). $)
    rankval $p |- ( rank ` A ) = |^| { x e. On | A e. ( R1 ` suc x ) } $=
      ( vz vy crnk cfv cv csuc cr1 wcel con0 crab cint wceq copab df-rank
      fveq1i c0 wne cvv wrex rankwflem ax-mp rabn0 mpbir intex mpbi eleq1
      rabbisdv inteqd fvopab eqtr ) BFGBDHEHZAHIJGZKZALMZNZOEDPZGBUOKZALMZNZBFU
      SEDAQREDBURVBCVASTZVBUAKVCUTALUBZBUAKVDCABUAUCUDUTALUEUFVAUGUHUNBOZUQVAVE
      UPUTALUNBUOUIUJUKULUM $.
      $( [24-Sep-2003] $)
  $}

  ${
    $d x y A $.
    $( Value of the rank function.  Definition 9.14 of [TakeutiZaring]
       p. 79 (proved as a theorem from our definition).  This
       variant of ~ rankval expresses the class existence requirement as an
       antecedent instead of a hypothesis. $)
    rankvalg $p |- ( A e. B ->
                   ( rank ` A ) = |^| { x e. On | A e. ( R1 ` suc x ) } ) $=
      ( vy cv crnk cfv csuc cr1 wcel con0 crab cint wceq fveq2 eleq1 rabbisdv
      inteqd eqeq12d visset rankval vtoclg ) DEZFGZUCAEHIGZJZAKLZMZNBFGZBUEJZAK
      LZMZNDBCUCBNZUDUIUHULUCBFOUMUGUKUMUFUJAKUCBUEPQRSAUCDTUAUB $.
      $( [5-Oct-2003] $) $( [5-Oct-2003] $)

    $( Value of an alternate definition of the rank function.  Definition of
       [BellMachover] p. 478. $)
    rankval2 $p |- ( A e. B ->
                   ( rank ` A ) = |^| { x e. On | A (_ ( R1 ` x ) } ) $=
      ( wcel crnk cfv cv csuc cr1 con0 crab cint wss rankvalg cpw r1suc eleq2d
      cvv wb fvex elpw2g ax-mp syl6bb rabbii inteqi syl6eq ) BCDBEFBAGZHIFZDZAJ
      KZLBUGIFZMZAJKZLABCNUJUMUIULAJUGJDZUIBUKOZDZULUNUHUOBUGPQUKRDUPULSUGITBUK
      RUAUBUCUDUEUF $.
      $( [8-Oct-2003] $) $( [8-Oct-2003] $)
  $}

  ${
    $d x A $.
    $( The rank of a set is an ordinal number.  Proposition 9.15(1) of
       [TakeutiZaring] p. 79. $)
    rankon $p |- ( rank ` A ) e. On $=
      ( vx cvv wcel crnk cfv con0 cv csuc cr1 crab cint rankvalg c0 wne wrex
      rankwflem rabn0 sylibr wss ssrab2 oninton mpan syl eqeltrd wn fvprc
      0elon syl6eqel pm2.61i ) ACDZAEFZGDUKULABHIJFDZBGKZLZGBACMUKUNNOZUOGDZUKU
      MBGPUPBACQUMBGRSUNGTUPUQUMBGUAUNUBUCUDUEUKUFULNGAEUGUHUIUJ $.
      $( [5-Oct-2003] $) $( [5-Oct-2003] $)
  $}

  ${
    $d x y A $.
    rankid.1 $e |- A e. V $.
    $( Identity law for the rank function. $)
    rankid $p |- A e. ( R1 ` suc ( rank ` A ) ) $=
      ( vx vy cv csuc cr1 cfv wcel con0 crab cint crnk wrex cvv rankwflem
      ax-mp ax-17 hbrab1 hbint hbsuc hbfv hbel wceq suceq fveq2d eleq2d
      onminsb rankval fveq2i eleqtrr ) AACEZFZGHZIZCJKZLZFZGHZAMHZFZGHUOCJNZAUS
      IZAOIVBBCAOPQUOVCCCDDAUSDEZAICRCDURGVDGICRCDUQCDUPUOCDJSTUAUBUCULUQUDZUNU
      SAVEUMURGULUQUEUFUGUHQVAURGUTUQUDVAURUDCABUIUTUQUEQUJUK $.
      $( [4-Oct-2003] $) $( [3-Oct-2003] $)
  $}

  ${
    rankr1lem.1 $e |- A e. V $.
    $( Lemma for ~ rankr1 . $)
    rankr1lem $p |- ( B e. On ->
                    ( -. A e. ( R1 ` B ) -> B (_ ( rank ` A ) ) ) $=
      ( con0 wcel cr1 cfv wn crnk csuc wss wb rankon onsuc ontri1 mpan rankid
      wi r1ord3 ssel syl6 mpii sylbird con1d onsssuc mpan2 sylibrd ) BDEZABFGZE
      ZHBAIGZJZEZBUKKZUHUMUJUHUMHZULBKZUJULDEZUHUPUOLUKAMZNZULBOPUHUPAULFGZEZUJ
      ACQUHUPUTUIKZVAUJRUQUHUPVBRUSULBSPUTUIATUAUBUCUDUHUKDEUNUMLURBUKUEUFUG $.
      $( [6-Oct-2003] $) $( [6-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    rankr1.1 $e |- A e. V $.
    $( A relationship between the rank function and the cumulative hierarchy
       of sets function ` R1 ` .  Proposition 9.15(2) of [TakeutiZaring]
       p. 79. $)
    rankr1 $p |- ( B = ( rank ` A ) <->
                 ( -. A e. ( R1 ` B ) /\ A e. ( R1 ` suc B ) ) ) $=
      ( vx vy crnk cfv wceq cr1 wcel wn csuc wa con0 rankon eleq1 mpbiri word
      wi eloni c0 cv wrex wlim w3o ordzsl noel fveq2 r10 syl6eq eleq2d mtbiri
      a1d wss visset sucid eleq2 adantl wb ontri1 con2bid eleq1a imp suceloni
      sylan pm3.26 sylanc mpbid adantr crab cint rankval eqeq2i biimp sseq1d
      suceq fveq2d onintss syl5bir biimpa sylan2i exp4d com3l imp31 mtod exp31
      r19.23aiv onel syl6bi anc2li r1ord2 mpi syl sseld sseq1i syl6ibr syld
      sseq1 biimprd sylan9r sylibd syl6 con2 pm2.43a con2i ciun cvv r1lim fvex
      eliun syl6bb mtbird expcom 3jaoi sylbi mpcom wne ssrab2 rankwflem ax-mp
      rabn0 mpbir onint mp2an eqeltr elrab pm3.27bd jca rankr1lem com12 cdm
      ndmfv a3i wfn r1fnon fndm eleq2i sucelon bitr4 sylib syl5 syl5ss eqssd
      impbi ) BAFGZHZABIGZJZKZABLZIGZJZMZUUFUUIUULBNJZUUFUUIUUFUUNUUENJAOZBUUEN
      PQZUUNBRZUUFUUISZBTUUQBUAHZBDUBZLZHZDNUCZBUDZUEUURDBUFUUSUURUVCUVDUUSUUIU
      UFUUSUUHAUAJZAUGZUUSUUGUAAUUSUUGUAIGUABUAIUHUIUJUKULUMUVBUURDNUUTNJZUVBUU
      FUUIUVGUVBMZUUFMUUHBUUTUNZUVHUVIKZUUFUVHUUTBJZUVJUVBUVKUVGUVBUVKUUTUVAJZU
      UTDUOUPZBUVAUUTUQQURUUNUVGUVKUVJUSUVHUUNUVGMUVIUVKBUUTUTZVAUVANJZUVBUUNUV
      GUVOUVBUUNUVANBVBVCUUTVDZVEUVGUVBVFVGVHVIUVGUVBUUFUUHUVISZUUFUVGUVBUVQUUF
      UVGUVBUUHUVIUUFUVGAUVAIGZJZUVIUVBUUHMUUFUVIAEUBZLZIGZJZENVJZVKZUUTUNZUVGU
      VSMUUFBUWEUUTUUFBUWEHUUEUWEBEACVLZVMVNVOUVGUVSUWFUWCUVSEUUTUVTUUTHZUWBUVR
      AUWHUWAUVAIUVTUUTVPVQUKVRZVCVSUVBUUHUVSUVBUUGUVRABUVAIUHUKVTWAWBWCWDWEWFW
      GUUFUVDUUIUUFUVDMZUUHAUUTIGZJZDBUCZUUFUWMKUVDUWMUUFUWLUUFKZDBUWLUVKUWNUVK
      UWLUUFUVKKZSUVKUWNSUUFUVKUWLUWOUUFUVKUUFUVGMZUWLUWOSUUFUVKUVGUUFUVKUUTUUE
      JUVGBUUEUUTUQUUEUUTUUOWHWIWJUWPUWLUVIUWOUVGUWLUUEUUTUNZUUFUVIUVGUWLUVSUWQ
      UVGUWKUVRAUVGUVOUWKUVRUNZUVPUVOUVLUWRUVMUUTUVAWKWLWMWNUVGUVSUWFUWQUWIUUEU
      WEUUTUWGWOWPWQUUFUVIUWQBUUEUUTWRWSWTUUNUVGUVIUWOUSUUFUVNUUPVEXAXBWCUUFUVK
      XCXBXDWGXEVIUWJUUHADBUWKXFZJUWMUWJUUGUWSABXGJZUVDUUGUWSHUUFDBXGXHUUFUWTUU
      EXGJAFXIBUUEXGPQVEUKDABUWKXJXKXLXMXNXOWMXPUUFBUWDJZUULUUFUXAUUEUWDJUUEUWE
      UWDUWGUWDNUNUWDUAXQZUWEUWDJUWCENXRUXBUWCENUCZAXGJUXCCEAXGXSXTUWCENYAYBUWD
      YCYDYEBUUEUWDPQUXAUUNUULUWCUULEBNUVTBHZUWBUUKAUXDUWAUUJIUVTBVPVQUKZYFYGWM
      YHUUMBUUEUUIUULBUUEUNZUUIUUNUXFUULUUNUUIUXFABCYIYJUULUUJIYKZJZUUNUXHUULUX
      HKZUULUVEUVFUXIUUKUAAUUJIYLUKULYMUXHUUJNJUUNUXGNUUJINYNUXGNHYONIYPXTYQBYR
      YSYTZUUAVCUULUUEBUNUUIUULUWEBUUEUUNUULUWEBUNUXJUWCUULEBUXEVRXPUWGUUBURUUC
      UUD $.
      $( [7-Oct-2003] $) $( [6-Oct-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A relationship between the rank function and the cumulative hierarchy
       of sets function ` R1 ` .  Proposition 9.15(2) of [TakeutiZaring]
       p. 79. $)
    rankr1g $p |- ( A e. C -> ( B = ( rank ` A ) <->
                 ( -. A e. ( R1 ` B ) /\ A e. ( R1 ` suc B ) ) ) ) $=
      ( vx cv crnk cfv wceq cr1 wcel wn csuc wa wb fveq2 eqeq2d eleq1 negbid
      anbi12d bibi12d visset rankr1 vtoclg ) BDEZFGZHZUDBIGZJZKZUDBLIGZJZMZNBAF
      GZHZAUGJZKZAUJJZMZNDACUDAHZUFUNULURUSUEUMBUDAFOPUSUIUPUKUQUSUHUOUDAUGQRUD
      AUJQSTUDBDUAUBUC $.
      $( [8-Oct-2003] $) $( [6-Oct-2003] $)
  $}

  ${
    ssrankr1.1 $e |- A e. V $.
    $( A relationship between an ordinal number less than or equal to a rank,
       and the cumulative hierarchy of sets ` R1 ` .  Proposition 9.15(3) of
       [TakeutiZaring] p. 79. $)
    ssrankr1 $p |- ( B e. On ->
                   ( B (_ ( rank ` A ) <-> -. A e. ( R1 ` B ) ) ) $=
      ( con0 wcel crnk cfv wss cr1 wn wa csuc wceq eqid rankr1 mpbi pm3.26i wi
      rankon r1ord3 mpan2 imp sseld mtoi ex rankr1lem impbid ) BDEZBAFGZHZABIGZ
      EZJZUHUJUMUHUJKZULAUIIGZEZUPJZAUILIGEZUIUIMUQURKUINAUICOPQUNUKUOAUHUJUKUO
      HZUHUIDEUJUSRASBUITUAUBUCUDUEABCUFUG $.
      $( [8-Oct-2003] $) $( [8-Oct-2003] $)
  $}

  ${
    rankr1a.1 $e |- A e. V $.
    $( A relationship between rank and ` R1 ` , clearly equivalent to
       ~ ssrankr1 and friends through trichotomy, but in Raph's opinion
       considerably more intuitive.  See ~ rankr1b for the subset verion.
       (Contributed by Raph Levien, 29-May-04.) $)
    rankr1a $p |- ( B e. On -> ( A e. ( R1 ` B ) <-> ( rank ` A ) e. B ) ) $=
      ( con0 wcel cr1 cfv crnk wss wn ssrankr1 wb rankon ontri1 mpan2 bitr3d
      con4bid ) BDEZABFGEZAHGZBEZRBTIZSJUAJZABCKRTDEUBUCLAMBTNOPQ $.
      $( [30-May-2004] $) $( [29-May-2004] $)
  $}

  ${
    $d x y A $.
    $( The value of the cumulative hierarchy of sets function expressed in
       terms of rank.  Definition 15.19 of [Monk1] p. 113. $)
    r1val2 $p |- ( A e. On -> ( R1 ` A ) = { x | ( rank ` x ) e. A } ) $=
      ( con0 wcel cv crnk cfv cr1 visset rankr1a abbi2dv ) BCDAEZFGBDABHGLBAIJK
      $.
      $( [27-Jun-2004] $) $( [30-Nov-2003] $)

    $( The value of the cumulative hierarchy of sets function expressed in
       terms of rank.  Theorem 15.18 of [Monk1] p. 113. $)
    r1val3 $p |- ( A e. On ->
                 ( R1 ` A ) = U_ x e. A P~ { y | ( rank ` y ) e. x } ) $=
      ( con0 wcel cr1 cfv cv cpw ciun crnk cab r1val1 wceq wral wa onelon
      r1val2 pweq 3syl r19.21aiva iuneq2 syl eqtrd ) CDEZCFGACAHZFGZIZJZACBHKGU
      FEBLZIZJZACMUEUHUKNZACOUIULNUEUMACUEUFCEPUFDEUGUJNUMCUFQBUFRUGUJSTUAACUHU
      KUBUCUD $.
      $( [3-Jul-2004] $) $( [30-Nov-2003] $)
  $}

  ${
    rankel.1 $e |- B e. V $.
    $( The membership relation is inherited by the rank function.  Proposition
       9.16 of [TakeutiZaring] p. 79. $)
    rankel $p |- ( A e. B -> ( rank ` A ) e. ( rank ` B ) ) $=
      ( wcel crnk cfv csuc cr1 wn wceq wa eqid rankr1g mpbii pm3.26d wss wi
      cpw con0 rankon r1suc ax-mp eleq2i elpw bitr ssel sylbi com12 mtod wb
      ontri1 mp2an word onord ordsucsssuc rankid onsuc r1ord3 sseld mpi sylbir
      nsyl2 ) ABDZBAEFZGZHFZDZVDBEFZDZVCVGAVDHFZDZVCVKIZAVFDZVCVDVDJVLVMKVDLAVD
      BMNOVGVCVKVGBVJPZVCVKQVGBVJRZDVNVFVOBVDSDZVFVOJATZVDUAUBUCBVJCUDUEBVJAUFU
      GUHUIVIIZVHVDPZVGVHSDVPVSVRUJBTZVQVHVDUKULVSVHGZVEPZVGVHUMVDUMVSWBUJVHVTU
      NVDVQUNVHVDUOULWBBWAHFZDVGBCUPWBWCVFBWASDVESDWBWCVFPQVHVTUQVDVQUQWAVEURUL
      USUTUGVAVB $.
      $( [9-Oct-2003] $) $( [4-Oct-2003] $)
  $}

  ${
    $d x y A $.
    rankval3.1 $e |- A e. V $.
    $( The value of the rank function expressed recursively:  the rank of a set
       is the smallest ordinal number containing the ranks of all members of
       the set.  Proposition 9.17 of [TakeutiZaring] p. 79. $)
    rankval3 $p |- ( rank ` A ) =
                  |^| { x e. On | A. y e. A ( rank ` y ) e. x } $=
      ( crnk cfv cv wcel wral con0 crab cint csuc cr1 rankval wss visset
      rankid wi word eloni ordsucss syl rankon onsuc r1ord3 mpan syld ssel
      syl6 mpii r19.20sdv cpw r1suc eleq2d elpw dfss3 bitr syl6bb sylibrd
      ss2rabi intss ax-mp eqsstr rankel rgen wceq eleq2 ralbidv onintss mp2
      eqssi ) CEFZBGZEFZAGZHZBCIZAJKZLZVMCVPMNFZHZAJKZLZVTACDOVSWCPWDVTPVRWBAJV
      PJHZVRVNVPNFZHZBCIZWBWEVQWGBCWEVQVNVOMZNFZHZWGVNBQRWEVQWJWFPZWKWGSWEVQWIV
      PPZWLWEVPTVQWMSVPUAVOVPUBUCWIJHWEWMWLSVOVNUDUEWIVPUFUGUHWJWFVNUIUJUKULWEW
      BCWFUMZHZWHWEWAWNCVPUNUOWOCWFPWHCWFDUPBCWFUQURUSUTVAVSWCVBVCVDVMJHVOVMHZB
      CIZVTVMPCUDWPBCVNCDVEVFVRWQAVMVPVMVGVQWPBCVPVMVOVHVIVJVKVL $.
      $( [11-Oct-2003] $) $( [11-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Any class whose elements have bounded rank is a set.  Proposition 9.19
       of [TakeutiZaring] p. 80. $)
    bndrank $p |- ( E. x e. On A. y e. A ( rank ` y ) (_ x -> A e. V ) $=
      ( cv crnk cfv wss wral cvv wcel con0 csuc cr1 visset rankid wi word wb
      eloni rankon onord ordsucsssuc mpan syl suceloni onsuc r1ord3 sylbid
      ssel syl6 mpii r19.20sdv dfss3 fvex ssex sylbir r19.23aiv ) BDZEFZADZGZBC
      HZCIJZAKUTKJZVBURUTLZMFZJZBCHZVCVDVAVGBCVDVAURUSLZMFZJZVGURBNOVDVAVJVFGZV
      KVGPVDVAVIVEGZVLVDUTQZVAVMRZUTSUSQVNVOUSURTZUAUSUTUBUCUDVDVEKJZVMVLPZUTUE
      VIKJVQVRUSVPUFVIVEUGUCUDUHVJVFURUIUJUKULVHCVFGVCBCVFUMCVFVEMUNUOUPUJUQ $.
      $( [20-Nov-2003] $) $( [13-Oct-2003] $)

    $( The elements of a proper class have unbounded rank.  Exercise 2 of
       [TakeutiZaring] p. 80. $)
    unbndrank $p |- ( -. A e. V -> A. x e. On E. y e. A x e. ( rank ` y ) ) $=
      ( cv crnk cfv wcel wrex con0 wral cvv wn wss wb rankon ontri1 mpan
      ralbidv ralnex syl6bb rexbiia rexnal bitr bndrank sylbir con1i ) ADZBDZEF
      ZGZBCHZAIJZCKGZULLZUIUGMZBCJZAIHZUMUQUKLZAIHUNUPURAIUGIGZUPUJLZBCJURUSUOU
      TBCUIIGUSUOUTNUHOUIUGPQRUJBCSTUAUKAIUBUCABCUDUEUF $.
      $( [21-Nov-2003] $) $( [13-Oct-2003] $)
  $}

  ${
    rankpw.1 $e |- A e. V $.
    $( The rank of a power set.  Part of Exercise 30 of [Enderton] p. 207. $)
    rankpw $p |- ( rank ` P~ A ) = suc ( rank ` A ) $=
      ( crnk cfv csuc cpw wceq cr1 wcel wn pwex rankr1 wss eqid pm3.26bd ax-mp
      pwid ssel mpi mto elpw mtbir con0 rankon r1suc eleq2i rankid eleqtr mpbi
      sspwb sseqtr4 mpbir onsuc eleqtrr mpbir2an eqcomi ) ACDZEZAFZCDZURUTGUSUR
      HDZIZJUSUREHDZIUSURABKZLVBUSUQHDZFZIZVGUSVEMZVHAVEIZUQUQGZVIJZUQNVJVKAVAI
      AUQBLOPVHAUSIVIABQUSVEARSTUSVEVDUAUBVAVFUSUQUCIVAVFGAUDZUQUEPZUFUBUSVAFZV
      CUSVNIUSVAMUSVFVAAVEMZUSVFMAVFIVOAVAVFABUGVMUHAVEBUAUIAVEUJUIVMUKUSVAVDUA
      ULURUCIVCVNGUQVLUMURUEPUNUOUP $.
      $( [22-Nov-2003] $) $( [22-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The rank of a set belongs to a limit ordinal iff the rank of its power
       set does. $)
    ranklim $p |- ( Lim B ->
                  ( ( rank ` A ) e. B <-> ( rank ` P~ A ) e. B ) ) $=
      ( vx cvv wcel wlim crnk cfv cpw wb wa csuc limsuc adantl cv wceq pweq
      fveq2d fveq2 suceq syl eqeq12d visset rankpw vtoclg eleq1d adantr bitr4d
      wn c0 fvprc pwexb negbii sylbi eqtr4d pm2.61ian ) ADEZBFZAGHZBEZAIZGHZBEZ
      JZUQURKUTUSLZBEZVCURUTVFJUQBUSMNUQVCVFJURUQVBVEBCOZIZGHZVGGHZLZPVBVEPCADV
      GAPZVIVBVKVEVLVHVAGVGAQRVLVJUSPVKVEPVGAGSVJUSTUAUBVGCUCUDUEUFUGUHUQUIZVDU
      RVMUSVBBVMUSUJVBAGUKVMVADEZUIVBUJPUQVNAULUMVAGUKUNUOUFUGUP $.
      $( [19-Sep-2006] $) $( [18-Sep-2006] $)

    $( A stronger property of ` R1 ` than ~ rankpw .  The latter merely
       proves that ` R1 ` of the successor is a power set, but here we prove
       that if ` A ` is in the cumulative hierarchy, then ` P~ A ` is in the
       cumulative hierarchy of the successor.  (Contributed by Raph Levien,
       29-May-04.) $)
    r1pw $p |- ( B e. On ->
                 ( A e. ( R1 ` B ) <-> P~ A e. ( R1 ` suc B ) ) ) $=
      ( vx cvv wcel con0 cr1 cfv cpw csuc wb wi cv wceq eleq1 pweq eleq1d
      bibi12d imbi2d crnk visset rankr1a word eloni ordsucelsuc syl bitrd
      rankpw eleq1i syl6bbr suceloni pwex bitr4d vtoclg wn elisset pwexb
      sylibr pm5.21ni a1d pm2.61i ) ADEZBFEZABGHZEZAIZBJZGHZEZKZLZVCCMZVDEZVLIZ
      VHEZKZLVKCADVLANZVPVJVCVQVMVEVOVIVLAVDOVQVNVFVHVLAPQRSVCVMVNTHZVGEZVOVCVM
      VLTHZJZVGEZVSVCVMVTBEZWBVLBCUAZUBVCBUCWCWBKBUDVTBUEUFUGVRWAVGVLWDUHUIUJVC
      VGFEVOVSKBUKVNVGVLWDULUBUFUMUNVBUOVJVCVEVBVIAVDUPVIVFDEVBVFVHUPAUQURUSUTV
      A $.
      $( [30-May-2004] $) $( [29-May-2004] $)
  $}

  ${
    $d A x y $. $d B x y $.
    $( MAY BE REVISED - can ranklim + rankr1a shorten this? $)
    $( The cumulative hierarchy of a limit ordinal is closed under power set.
       (Contributed by Raph Levien, 29-May-04.) $)
    r1pwcl $p |- ( Lim B -> ( A e. ( R1 ` B ) <-> P~ A e. ( R1 ` B ) ) ) $=
      ( vx vy cvv wcel wlim cr1 cfv cpw wb wi wa cv wrex csuc ciun r1lim
      eleq2d eliun syl6bb con0 onelon limelon sylan r1pw syl rexbidva wex
      limsuc anbi1d visset sucex wceq eleq1 fveq2 anbi12d cla4ev syl6bi
      19.23adv df-rex 3imtr4g cbvrexv syl6ibr adantl ex wss sssucid r1ord3 mpi
      sucelon sylan2b anidms sseld syl6 r19.22dv impbid 3bitrd bitr4d wn c0
      n0i fvprc nsyl2 pm5.21ni a1d pm2.61i ) BEFZBGZABHIZFZAJZWJFZKZLWHWIWNWHWI
      MZWKWLCNZHIZFZCBOZWMWOWKAWQFZCBOZWLWPPZHIZFZCBOZWSWOWKACBWQQZFXAWOWJXFACB
      ERZSCABWQTUAWOWTXDCBWOWPBFZMWPUBFZWTXDKBUBFXHXIWOBWPUCBEUDUEZAWPUFUGUHWOX
      EWSWIXEWSLWHWIXEWLDNZHIZFZDBOZWSWIXHXDMZCUIXKBFZXMMZDUIZXEXNWIXOXRCWIXOXB
      BFZXDMZXRWIXHXSXDBWPUJUKXQXTDXBWPCULUMXKXBUNZXPXSXMXDXKXBBUOYAXLXCWLXKXBH
      UPSUQURUSUTXDCBVAXMDBVAVBWRXMCDBWPXKUNWQXLWLWPXKHUPSVCVDVEWOWRXDCBWOXHXIW
      RXDLWOXHXIXJVFXIWQXCWLXIWQXCVGZXIXBUBFZYBXIXIYCMWPXBVGYBWPVHWPXBVIVJWPVKV
      LVMVNVOVPVQVRWOWMWLXFFWSWOWJXFWLXGSCWLBWQTUAVSVFWHVTWNWIWKWHWMWKWJWAUNZWH
      WJAWBBHWCZWDWMYDWHWJWLWBYEWDWEWFWG $.
      $( [1-Jun-2004] $) $( [1-Jun-2004] $)
  $}

  ${
    rankss.1 $e |- B e. V $.
    $( The subset relation is inherited by the rank function.  Exercise 1 of
       [TakeutiZaring] p. 80. $)
    rankss $p |- ( A (_ B -> ( rank ` A ) (_ ( rank ` B ) ) $=
      ( cpw wcel crnk cfv wss pwex rankel cvv wb elpw2g ax-mp csuc rankpw
      eleq2i con0 rankon onsssuc mp2an bitr4 3imtr3 ) ABDZEZAFGZUDFGZEZABHZUFBF
      GZHZAUDBCIJBKEUEUILCABKMNUHUFUJOZEZUKUGULUFBCPQUFREUJREUKUMLASBSUFUJTUAUB
      UC $.
      $( [25-Nov-2003] $) $( [25-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    ranksn.1 $e |- A e. V $.
    $( The rank of a singleton.  Theorem 15.17(v) of [Monk1] p. 112. $)
    ranksn $p |- ( rank ` { A } ) = suc ( rank ` A ) $=
      ( vy vx cv crnk cfv wcel csn wral con0 crab cint csuc wb wi wal wceq
      df-ral elsn imbi1i albii fveq2 eleq1d ceqsalv 3bitr a1i rabbii inteqi
      snex rankval3 rankon onsucmin ax-mp 3eqtr4 ) CEZFGZDEZHZCAIZJZDKLZMAFGZUR
      HZDKLZMZUTFGVCNZVBVEVAVDDKVAVDOURKHVAUPUTHZUSPZCQUPARZUSPZCQVDUSCUTSVIVKC
      VHVJUSCATUAUBUSVDCABVJUQVCURUPAFUCUDUEUFUGUHUIDCUTAUJUKVCKHVGVFRAULDVCUMU
      NUO $.
      $( [20-Jul-2004] $) $( [28-Nov-2003] $)

    $( The rank of a union.  Part of Theorem 15.17(iv) of [Monk1] p. 112. $)
    rankuni2 $p |- ( rank ` U. A ) = U_ x e. A ( rank ` x ) $=
      ( vy vz cuni crnk cfv cv ciun wcel wral con0 crab cint uniex rankval3
      wss wceq eleq2 ralbidv elrab fvex iunon rankon a1i mprg wrex eluni2
      ax-17 hbiu1 hbel ssiun2 sseld visset rankel syl5 r19.23ai sylbi rgen
      mpbir2an intss1 ax-mp eqsstr iunss elssuni rankss syl mprgbir eqssi ) BFZ
      GHZABAIZGHZJZVLDIZGHZEIZKZDVKLZEMNZOZVOEDVKBCPZQVOWAKZWBVORWDVOMKZVQVOKZD
      VKLZVTWGEVOMVRVOSVSWFDVKVRVOVQTUAUBVNMKZWEABABVNCVMGUCUDWHVMBKZVMUEUFUGWF
      DVKVPVKKVPVMKZABUHWFAVPBUIWJWFABAEEVQVOVRVQKAUJAEBVNUKULWIVQVNKWFWJWIVNVO
      VQABVNUMUNVPVMAUOUPUQURUSUTVAVOWAVBVCVDVOVLRVNVLRZABABVNVLVEWIVMVKRWKVMBV
      FVMVKWCVGVHVIVJ $.
      $( [21-Jul-2004] $) $( [30-Nov-2003] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    rankun.1 $e |- A e. V $.
    rankun.2 $e |- B e. V $.
    $( The rank of the union of two sets.  Theorem 15.17(iii) of [Monk1]
       p. 112. $)
    rankun $p |- ( rank ` ( A u. B ) ) = ( ( rank ` A ) u. ( rank ` B ) ) $=
      ( vx vz vy cun crnk cfv cv wcel wral wi con0 crab cint unex rankval3
      eleq2i visset elintrab bitr wo elun rankel elun1 syl elun2 jaoi sylbi
      rgen rankon onun wceq eleq2 ralbidv imbi12d rcla4v ax-mp mpi ssriv wss
      ssun1 rankss ssun2 unssi eqssi ) ABHZIJZAIJZBIJZHZEVJVMEKZVJLZFKZIJZGKZLZ
      FVIMZVNVRLZNZGOMZVNVMLZVOVNVTGOPQZLWCVJWEVNGFVIABCDRZSTVTGVNOEUAUBUCWCVQV
      MLZFVIMZWDWGFVIVPVILVPALZVPBLZUDWGVPABUEWIWGWJWIVQVKLWGVPACUFVQVKVLUGUHWJ
      VQVLLWGVPBDUFVQVLVKUIUHUJUKULVMOLWCWHWDNZNVKVLAUMBUMUNWBWKGVMOVRVMUOZVTWH
      WAWDWLVSWGFVIVRVMVQUPUQVRVMVNUPURUSUTVAUKVBVKVLVJAVIVCVKVJVCABVDAVIWFVEUT
      BVIVCVLVJVCBAVFBVIWFVEUTVGVH $.
      $( [25-Aug-2004] $) $( [26-Nov-2003] $)

    $( The rank of an unordered pair.  Part of Exercise 30 of [Enderton]
       p. 207. $)
    rankpr $p |- ( rank ` { A , B } ) = suc ( ( rank ` A ) u. ( rank ` B ) ) $=
      ( cpr crnk cfv csuc cun csn df-pr fveq2i snex rankun ranksn uneq12i
      3eqtr word wceq rankon onord ordsucun mp2an eqtr4 ) ABEZFGZAFGZHZBFGZHZIZ
      UGUIIHZUFAJZBJZIZFGUMFGZUNFGZIUKUEUOFABKLUMUNAMBMNUPUHUQUJACOBDOPQUGRUIRU
      LUKSUGATUAUIBTUAUGUIUBUCUD $.
      $( [19-Sep-2004] $) $( [28-Nov-2003] $)

    $( The rank of an ordered pair.  Part of Exercise 4 of [Kunen] p. 107. $)
    rankop $p |- ( rank ` <. A , B >. ) =
                 suc suc ( ( rank ` A ) u. ( rank ` B ) ) $=
      ( cop crnk cfv csn cpr cun csuc df-op fveq2i snex prex rankpr wceq wss
      snsspr ssequn1 mpbi rankun 3eqtr3 suceq ax-mp 3eqtr ) ABEZFGAHZABIZIZFGUH
      FGUIFGZJZKZAFGBFGJKZKZUGUJFABLMUHUIANZABOZPULUNQUMUOQUHUIJZFGUKULUNURUIFU
      HUIRURUIQABSUHUITUAMUHUIUPUQUBABCDPUCULUNUDUEUF $.
      $( [16-Oct-2006] $) $( [13-Sep-2006] $)
  $}

  $( Any set is a subset of the hierarchy of its rank. $)
  r1rankid $p |- ( A e. B -> A (_ ( R1 ` ( rank ` A ) ) ) $=
    ( wcel crnk cfv cr1 cpw wss csuc wn wceq wa eqid rankr1g mpbii pm3.27d
    con0 rankon r1suc ax-mp syl6eleq elpwg mpbid ) ABCZAADEZFEZGZCAUFHUDAUEIFEZ
    UGUDAUFCJZAUHCZUDUEUEKUIUJLUEMAUEBNOPUEQCUHUGKARUESTUAAUFBUBUC $.
    $( [9-Jan-2004] $) $( [14-Oct-2003] $)

  ${
    $d x y z A $.
    $( The rank of an ordinal number is itself.  Proposition 9.18 of
       [TakeutiZaring] p. 79 and its converse. $)
    rankonid $p |- ( A e. On <-> ( rank ` A ) = A ) $=
      ( vx vy vz con0 wcel crnk cfv wceq cv fveq2 id eqeq12d wral wss crab
      cint wb eleq1 r19.20si r19.15 syl dfss3 syl6bbr rabbisdv inteqd visset
      rankval3 syl5eq intmin sylan9eqr ex tfis3 rankon mpbii impbi ) AEFZAGHZAI
      ZBJZGHZUTIZCJZGHZVCIZUSBCAUTVCIZVAVDUTVCUTVCGKVFLMUTAIZVAURUTAUTAGKVGLMUT
      EFZVECUTNZVBVIVHVAUTDJZOZDEPZQZUTVIVDVJFZCUTNZDEPZQVMVAVIVPVLVIVOVKDEVIVO
      VCVJFZCUTNZVKVIVNVQRZCUTNVOVRRVEVSCUTVDVCVJSTVNVQCUTUAUBCUTVJUCUDUEUFDCUT
      BUGUHUIDUTEUJUKULUMUSUREFUQAUNURAESUOUP $.
      $( [25-Nov-2003] $) $( [14-Oct-2003] $)
  $}

  ${
    $d x A $.
    rankeq0.1 $e |- A e. V $.
    $( A set is empty iff its rank is empty. $)
    rankeq0 $p |- ( A = (/) <-> ( rank ` A ) = (/) ) $=
      ( vx c0 wceq crnk cfv fveq2 con0 wcel 0elon rankonid mpbi syl6eq cv cr1
      wss crab cint cvv rankval2 ax-mp eqeq1i wa wb ssrab2 onint0 sseq2d elrab
      biantrur r10 sseq2i bitr3 3bitr ss0 sylbi impbi ) ADEZAFGZDEZURUSDFGZDADF
      HDIJZVADEKDLMNUTACOZPGZQZCIRZSZDEZURUSVGDATJUSVGEBCATUAUBUCVHADQZURVHDVFJ
      ZVBADPGZQZUDZVIVFIQVHVJUEVECIUFVFUGUBVEVLCDIVCDEVDVKAVCDPHUHUIVMVLVIVBVLK
      UJVKDAUKULUMUNAUOUPUPUQ $.
      $( [23-Sep-2006] $) $( [18-Sep-2006] $)
  $}

  ${
    $d x y z A $.
    $( The rank of the hierarchy of an ordinal number is itself. $)
    rankr1id $p |- ( A e. On <-> ( rank ` ( R1 ` A ) ) = A ) $=
      ( vx vy con0 wcel cr1 cfv crnk wceq cv fveq2 fveq2d id eqeq12d wss crab
      cint r1ord3 ss2rabdv intss syl intmin sseqtrd cvv fvex rankval2 ax-mp
      syl5ss rankonid visset r1rankid sseq2d mpbii rankss sseq1 mpbid sylbi
      eqssd vtoclga rankon eleq1 impbi ) ADEZAFGZHGZAIZBJZFGZHGZVGIVFBADVGAIZVI
      VEVGAVJVHVDHVGAFKLVJMNVGDEZVIVGVKVHCJZFGOZCDPZQZVGVIVKVOVGVLOZCDPZQZVGVKV
      QVNOVOVROVKVPVMCDVGVLRSVQVNTUACVGDUBUCVHUDEVIVOIVGFUEZCVHUDUFUGUHVKVGHGZV
      GIZVGVIOZVGUIWAVTVIOZWBWAVGVHOZWCWAVGVTFGZOZWDVGUDEWFBUJVGUDUKUGWAWEVHVGV
      TVGFKULUMVGVHVSUNUAVTVGVIUOUPUQURUSVFVEDEVCVDUTVEADVAUMVB $.
      $( [25-Nov-2003] $) $( [14-Oct-2003] $)

    $( The rank of a union.  Part of Exercise 4 of [Kunen] p. 107. $)
    rankuni $p |- ( rank ` U. A ) = U. ( rank ` A ) $=
      ( vx vy vz cvv wcel cuni crnk cfv wceq cv unieq fveq2d fveq2 unieqd
      eqeq12d wrex cab ciun visset rankuni2 fvex dfiun2 eqtr wss wa wex df-rex
      rankel anim1i 19.22i 19.42v eleq1 pm5.32ri exbii pm3.26 cr1 con0 rankon
      onel rankr1id sylib eqcomd eqeq2d cla4ev syl ancli impbi 3bitr3 sylbi
      abssi uniss ax-mp eqsstr csuc cpw pwuni uniex pwex rankss rankpw sseqtr
      onunisuc eqssi vtoclg wn c0 uniexb negbii fvprc uni0 syl6eqr eqtr4d
      pm2.61i ) AEFZAGZHIZAHIZGZJZBKZGZHIZXAHIZGZJWTBAEXAAJZXCWQXEWSXFXBWPHXAAL
      MXFXDWRXAAHNOPXCXEXCCKZDKZHIZJZDXAQZCRZGZXEXCDXAXISXMDXABTZUADCXAXIXHHUBU
      CUDXLXDUEXMXEUEXKCXDXKXHXAFZXJUFZDUGZXGXDFZXJDXAUHXQXIXDFZXJUFZDUGZXRXPXT
      DXOXSXJXHXAXNUIUJUKXRXJUFZDUGXRXJDUGZUFZYAXRXRXJDULYBXTDXJXRXSXGXIXDUMUNU
      OYDXRXRYCUPXRYCXRXGXGUQIZHIZJZYCXRYFXGXRXGURFYFXGJXDXGXAUSUTXGVAVBVCXJYGD
      YEXGUQUBXHYEJXIYFXGXHYEHNVDVEVFVGVHVIVBVJVKXLXDVLVMVNXEXCVOZGZXCXDYHUEXEY
      IUEXDXBVPZHIZYHXAYJUEXDYKUEXAVQXAYJXBXAXNVRZVSVTVMXBYLWAWBXDYHVLVMXCXBUSW
      CWBWDWEWOWFZWQWGGZWSYMWQWGYNYMWPEFZWFWQWGJWOYOAWHWIWPHWJVJWKWLYMWRWGAHWJO
      WMWN $.
      $( [28-Sep-2006] $) $( [15-Sep-2006] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    rankr1b.1 $e |- A e. V $.
    $( A relationship between rank and ` R1 ` .  See ~ rankr1a for the
       membership version. $)
    rankr1b $p |- ( B e. On -> ( A (_ ( R1 ` B ) <-> ( rank ` A ) (_ B ) ) $=
      ( con0 wcel cr1 cfv wss crnk wceq rankr1id biimp sseq2d fvex rankss
      syl5bi wi rankon r1ord3 mpan cvv r1rankid ax-mp sstr syl6 impbid ) BDEZAB
      FGZHZAIGZBHZUGUJUHIGZHUKUIUGULBUJUGULBJBKLMAUHBFNOPUGUKUJFGZUHHZUIUJDEUGU
      KUNQARUJBSTAUMHZUNUIAUAEUOCAUAUBUCAUMUHUDTUEUF $.
      $( [22-Sep-2006] $) $( [15-Sep-2006] $)

    $( The rank of a successor. $)
    ranksuc $p |- ( rank ` suc A ) = suc ( rank ` A ) $=
      ( csuc crnk cfv csn cun df-suc fveq2i snex rankun ranksn uneq2i wss wceq
      sssucid ssequn1 mpbi eqtr 3eqtr ) ACZDEAAFZGZDEADEZUBDEZGZUDCZUAUCDAHIAUB
      BAJKUFUDUGGZUGUEUGUDABLMUDUGNUHUGOUDPUDUGQRST $.
      $( [13-Dec-2006] $) $( [18-Sep-2006] $)

    $( Upper bound of the rank of a union.  Part of Exercise 30 of [Enderton]
       p. 207. $)
    rankuniss $p |- ( rank ` U. A ) (_ ( rank ` A ) $=
      ( cuni crnk cfv rankuni word wss rankon onord orduniss ax-mp eqsstr ) ACD
      EADEZCZNAFNGONHNAIJNKLM $.
      $( [30-Nov-2003] $) $( [30-Nov-2003] $)

    $( The rank of a set is the supremum of the successors of the ranks of its
       members.  Exercise 9.1 of [Jech] p. 72.  Also a special case of Theorem
       7V(b) of [Enderton] p. 204. $)
    rankval4 $p |- ( rank ` A ) = U_ x e. A suc ( rank ` x ) $=
      ( vy crnk cfv cv csuc ciun cr1 wss wcel wi ax-17 hbiu1 hbfv dfss2f
      visset rankid ssiun2 con0 rankon onsuc fvex sucex iunon a1i mprg r1ord3
      mp2an syl sseld mpi mpgbir rankss ax-mp crab cint mpan ss2rabi intss cvv
      wceq rankval2 intmin eqcomi 3sstr4 sstri iunss rankel onsucss sylib
      mprgbir eqssi ) BEFZABAGZEFZHZIZVOVSJFZEFZVSBVTKZVOWAKWBVPBLZVPVTLZMAADBV
      TDGZBLANADVSJWEJLANADBVROPQWCVPVRJFZLWDVPARSWCWFVTVPWCVRVSKZWFVTKZABVRTVR
      UALZVSUALZWGWHMVQVPUBZUCZWIWJABABVRCVQVPEUDUEUFWIWCWLUGUHZVRVSUIUJUKULUMU
      NBVTVSJUDZUOUPVTWEJFKZDUAUQZURZVSWEKZDUAUQZURZWAVSWSWPKWQWTKWRWODUAWJWEUA
      LWRWOMWMVSWEUIUSUTWSWPVAUPVTVBLWAWQVCWNDVTVBVDUPWTVSWJWTVSVCWMDVSUAVEUPVF
      VGVHVSVOKVRVOKZABABVRVOVIWCVQVOLXAVPBCVJVQVOWKBUBVKVLVMVN $.
      $( [7-Nov-2004] $) $( [12-Oct-2003] $)

    $( The rank of a set is bounded by a bound for the successor of its
       members. $)
    rankbnd $p |- ( A. x e. A suc ( rank ` x ) (_ B <-> ( rank ` A ) (_ B ) $=
      ( crnk cfv wss cv csuc ciun wral rankval4 sseq1i iunss bitr2 ) BEFZCGABAH
      EFIZJZCGQCGABKPRCABDLMABQCNO $.
      $( [21-Sep-2006] $) $( [18-Sep-2006] $)

    $( The rank of a set is bounded by the successor of a bound for its
       members. $)
    rankbnd2 $p |- ( B e. On -> ( A. x e. A ( rank ` x ) (_ B <->
                  ( rank ` A ) (_ suc B ) ) $=
      ( con0 wcel crnk cfv cuni wss csuc cv wral word wb eloni rankon onss
      ordunisssuc mpan syl ciun rankuni rankuni2 eqtr3 sseq1i iunss bitr2
      syl5bb ) CEFZBGHZIZCJZUKCKJZALGHZCJABMZUJCNZUMUNOZCPUKEJUQURUKBQRUKCSTUAU
      MABUOUBZCJUPULUSCBIGHULUSBUCABDUDUEUFABUOCUGUHUI $.
      $( [5-Jan-2007] $) $( [15-Sep-2006] $)

    $( A relationship that can be used for computation of rank. $)
    rankc1 $p |- ( A. x e. A ( rank ` x ) e. ( rank ` U. A ) <->
                 ( rank ` A ) = ( rank ` U. A ) ) $=
      ( crnk cfv cuni wss wa cv wcel wral wceq rankuniss biantru csuc ciun
      iunss rankval4 sseq1i rankon onsucss ralbii 3bitr4r eqss 3bitr4 ) BDEZBFZ
      DEZGZUIUHUFGZHAIZDEZUHJZABKZUFUHLUJUIBCMNABULOZPZUHGUOUHGZABKUIUNABUOUHQU
      FUPUHABCRSUMUQABULUHUKTUGTUAUBUCUFUHUDUE $.
      $( [16-Jan-2007] $) $( [16-Sep-2006] $)

    $( A relationship that can be used for computation of rank. $)
    rankc2 $p |- ( E. x e. A ( rank ` x ) = ( rank ` U. A ) ->
                 ( rank ` A ) = suc ( rank ` U. A ) ) $=
      ( cv crnk cfv cuni wceq wrex csuc wss cpw pwuni uniex pwex rankss ax-mp
      rankpw sseqtr a1i wcel eleq1 biimpd rankel syl5com r19.23aiv rankon
      onsucss sylib eqssd ) ADZEFZBGZEFZHZABIZBEFZUNJZUQURKUPUQUMLZEFZURBUSKUQU
      TKBMBUSUMBCNZOPQUMVARSTUPUNUQUAZURUQKUOVBABUOULUQUAZVBUKBUAUOVCVBULUNUQUB
      UCUKBCUDUEUFUNUQUMUGBUGUHUIUJ $.
      $( [?] $) $( [16-Sep-2006] $)
  $}

  ${
    rankelun.1 $e |- A e. V $.
    rankelun.2 $e |- B e. V $.
    rankelun.3 $e |- C e. V $.
    rankelun.4 $e |- D e. V $.
    $( Rank membership is inherited by union. $)
    rankelun $p |- ( ( ( rank ` A ) e. ( rank ` C ) /\ ( rank ` B ) e.
          ( rank ` D ) ) -> ( rank ` ( A u. B ) ) e. ( rank ` ( C u. D ) ) ) $=
      ( crnk cfv wcel wa cun cr1 csuc wss cpr elun1 elun2 anim12i prss sylib
      fvex unex rankss syl rankpr wceq rankun suceq ax-mp eqtr4 syl5ssr cvv wi
      sucssel con0 wb rankon rankr1a syl2anbr rankr1id mpbi uneq12i 3eqtr4
      syl6eleq ) AIJZCIJZKZBIJZDIJZKZLABMZIJZVHNJZVKNJZMZIJZCDMIJZAVOKZBVPKZVNV
      RKZVIVLVTWALZVNOZVRPZWBWCABQZIJZVRWDWCWFVQPZWGVRPWCAVQKZBVQKZLWHVTWIWAWJA
      VOVPRBVPVOSTABVQEFUAUBWFVQVOVPVHNUCZVKNUCZUDUEUFWGVGVJMZOZWDABEFUGVNWMUHW
      DWNUHABEFUIVNWMUJUKULUMVNUNKWEWBUOVMIUCVNVRUNUPUKUFVHUQKZVTVIURCUSZAVHEUT
      UKVKUQKZWAVLURDUSZBVKFUTUKVAVOIJZVPIJZMVHVKMVRVSWSVHWTVKWOWSVHUHWPVHVBVCW
      QWTVKUHWRVKVBVCVDVOVPWKWLUICDGHUIVEVF $.
      $( [20-Sep-2006] $) $( [18-Sep-2006] $)

    $( Rank membership is inherited by unordered pairs. $)
    rankelpr $p |- ( ( ( rank ` A ) e. ( rank ` C ) /\ ( rank ` B ) e.
            ( rank ` D ) ) -> ( rank ` { A , B } ) e. ( rank ` { C , D } ) ) $=
      ( crnk cfv wcel wa cun csuc cpr rankelun rankun syl6eleq syl5eqelr word
      wb rankon onun onord ordsucelsuc ax-mp sylib rankpr syl6eleqr syl5eqel )
      AIJZCIJZKBIJZDIJZKLZUKUMMZNZCDOIJZABOIJUOUQULUNMZNZURUOUPUSKZUQUTKZUOABMI
      JZUSUPUOVCCDMIJUSABCDEFGHPCDGHQRABEFQSUSTVAVBUAUSULUNCUBDUBUCUDUPUSUEUFUG
      CDGHUHUIABEFUHUJ $.
      $( [23-Oct-2006] $) $( [18-Sep-2006] $)

    $( Rank membership is inherited by ordered pairs. $)
    rankelop $p |- ( ( ( rank ` A ) e. ( rank ` C ) /\ ( rank ` B ) e.
        ( rank ` D ) ) -> ( rank ` <. A , B >. ) e. ( rank ` <. C , D >. ) ) $=
      ( crnk cfv wcel wa cpr csuc cop rankelpr word wb rankon onord
      ordsucelsuc ax-mp sylib cun wceq rankpr suceq rankop eqtr4 syl6eleq
      syl5eqel ) AIJZCIJZKBIJZDIJZKLZABMIJZNZCDOIJZABOIJZUPURCDMZIJZNZUSUPUQVBK
      ZURVCKZABCDEFGHPVBQVDVERVBVASTUQVBUAUBUCVCUMUOUDNZNZUSVBVFUEVCVGUECDGHUFV
      BVFUGUBCDGHUHUIUJUTULUNUDNZNZURABEFUHUQVHUEURVIUEABEFUFUQVHUGUBUIUK $.
      $( [?] $) $( [18-Sep-2006] $)
  $}

  ${
    rankxpl.1 $e |- A e. V $.
    rankxpl.2 $e |- B e. V $.
    $( A lower bound on the rank of a cross product. $)
    rankxpl $p |- ( ( A X. B ) =/= (/) -> ( rank ` ( A u. B ) ) (_
                  ( rank ` ( A X. B ) ) ) $=
      ( cxp c0 wne cun crnk cfv cuni unixp fveq2d wss xpex uniex rankuniss
      sstri a1i eqsstr3d ) ABEZFGZABHZIJUAKZKZIJZUAIJZUBUEUCIABLMUFUGNUBUFUDIJU
      GUDUAABCDOZPQUAUHQRST $.
      $( [25-Sep-2006] $) $( [18-Sep-2006] $)

    $( An upper bound on the rank of a cross product. $)
    rankxpu $p |- ( rank ` ( A X. B ) ) (_
                 suc suc ( rank ` ( A u. B ) ) $=
      ( cxp crnk cfv cun cpw csuc wss xpsspw unex pwex rankss ax-mp rankpw
      wceq suceq eqtr sseqtr ) ABEZFGZABHZIZIZFGZUDFGJZJZUBUFKUCUGKABLUBUFUEUDA
      BCDMZNZNOPUGUEFGZJZUIUEUKQULUHRUMUIRUDUJQULUHSPTUA $.
      $( [24-Sep-2006] $) $( [18-Sep-2006] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    rankxplim.1 $e |- A e. V $.
    rankxplim.2 $e |- B e. V $.
    $( The rank of a cross product when the rank of the union of its
       arguments is a limit ordinal.  Part of Exercise 4 of [Kunen] p. 107.
       See ~ rankxpsuc for the successor case. $)
    rankxplim $p |- ( ( Lim ( rank ` ( A u. B ) ) /\ ( A X. B ) =/= (/) ) ->
                    ( rank ` ( A X. B ) ) = ( rank ` ( A u. B ) ) ) $=
      ( vx vy vz cun crnk cfv wlim cxp c0 wne wa wss cv cop csuc wral wcel cpw
      visset rankelun rankel syl2an adantl wb ranklim bitrd adantr mpbid cpr
      cuni pwuni wceq uniop pweq ax-mp sseqtr unipr sspwb mpbi sstri unex pwex
      rankss con0 wi rankon ontr2 mp2an mpan syl onsucss sylib ex r19.21aivv
      fveq2 suceq sseq1d ralxp xpex rankbnd bitr3 rankxpl eqssd ) ABHZIJZKZABLZ
      MNZOWKIJZWIWJWMWIPZWLWJEQZFQZRZIJZSZWIPZFBTEATZWNWJWTEFABWJWOAUAZWPBUAZOZ
      WTWJXDOZWRWIUAZWTXEWOWPHZUBZUBZIJZWIUAZXFXEXGIJWIUAZXKXDXLWJWOIJAIJUAWPIJ
      BIJUAXLXBXCWOWPABEUCZFUCZCDUDWOACUEWPBDUEUFUGWJXLXKUHXDWJXLXHIJWIUAXKXGWI
      UIXHWIUIUJUKULWRXJPZXKXFWQXIPXOWQWOWPUMZUBZXIWQWQUNZUBZXQWQUOXRXPUPXSXQUP
      WOWPUQXRXPURUSUTXPXHPXQXIPXPXPUNZUBZXHXPUOXTXGUPYAXHUPWOWPXMXNVAXTXGURUSU
      TXPXHVBVCVDWQXIXHXGWOWPXMXNVEVFVFVGUSWRVHUAWIVHUAXOXKOXFVIWQVJZWHVJZWRXJW
      IVKVLVMVNWRWIYBYCVOVPVQVRXAGQZIJZSZWIPZGWKTWNYGWTGEFABYDWQUPZYFWSWIYHYEWR
      UPYFWSUPYDWQIVSYEWRVTVNWAWBGWKWIABCDWCWDWEVPUKWLWIWMPWJABCDWFUGWG $.
      $( [26-Sep-2006] $) $( [19-Sep-2006] $)


    $( If the rank of a cross product is a limit ordinal, so is the rank of
       the union of its arguments. $)
    rankxplim2 $p |- ( Lim ( rank ` ( A X. B ) ) ->
                     Lim ( rank ` ( A u. B ) ) ) $=
      ( cxp c0 wne crnk cfv wlim cun wcel wceq wn 0ellim n0i df-ne xpex
      rankeq0 negbii bitr2 biimp 3syl cuni wb unixp fveq2d rankuni unieqi
      eqtr2 syl5eq limeq syl limuni2 syl5bi mpcom ) ABEZFGZUQHIZJZABKZHIZJZUTFU
      SLUSFMZNZURUSOUSFPVEURURUQFMZNVEUQFQVFVDUQABCDRSTUAUBUCURUSUDZUDZJZVCUTUR
      VHVBMVIVCUEURUQUDZUDZHIZVBVHURVKVAHABUFUGVLVJHIZUDVHVJUHVMVGUQUHUIUJUKVHV
      BULUMUTVGJVIUSUNVGUNUMUOUP $.
      $( [29-Sep-2006] $) $( [19-Sep-2006] $)

    $( The rank of a cross product is a limit ordinal iff its union is. $)
    rankxplim3 $p |- ( Lim ( rank ` ( A X. B ) ) <->
                     Lim U. ( rank ` ( A X. B ) ) ) $=
      ( vx vy cxp crnk cfv wlim cuni limuni2 c0 wceq wn cun cv csuc con0 wrex
      wa wo word w3o rankon onord ordzsl mpbi 3ori un00 olc adantl sylbir
      xpeq0 sylibr con3i xpex rankeq0 negbii unex 3imtr3 sylan ex orim12d
      ianor syl5ib imp wi pm3.26 wb wne rankxplim df-ne bitr sylan2br limeq
      syl mpbird expcom idd jaod adantr mpd wcel 0ellim n0i unieq uni0 syl6eq
      3syl wss onsuc elisseti sucid ontri1 mp2an con2bii rankxpu sstr mpan2
      mto simprl pm3.27 bitr2 sylib unixp fveq2d rankuni unieqi eqtr syl5reqr
      eqimss eqsstr3d adantrr limuni sseqtr4d cvv visset orduni ax-mp ordelsuc
      limsuc mpbid eqeltrd ordsucelsuc onsucuni2 mpan ad2antll eleqtrd a1d
      r19.23advv reeanv syl5ibr mtoi sylanc impbi ) ABGZHIZJZYRKZJZYRLYRMNZOZAB
      PZHIZEQZRZNZESTZYRFQZRNZFSTZUAZOZYSUUAUUCUUNUAUUEJZYSUBZYSUUCUUNUUPUUCUUI
      OZUULOZUBUUPUUNUUCUUQUUOUURYSUUCUUQUUOUUEMNZOZUUQUUOUUCUUSUUIUUOUUEUCUUSU
      UIUUOUDUUEUUDUEZUFEUUEUGUHUIYQMNZOZUUDMNZOUUCUUTUVDUVBUVDAMNZBMNZUBZUVBUV
      DUVEUVFUAUVGABUJUVFUVGUVEUVFUVEUKULUMABUNUOUPUVBUUBYQABCDUQURUSZUVDUUSUUD
      ABCDUTURUSVAVBVCUUCUURYSUUBUULYSYRUCZUUBUULYSUDYRYQUEZUFZFYRUGUHUIVCVDUUI
      UULVEVFVGUUCUUPYSVHUUNUUCUUOYSYSUUOUUCYSUUOUUCUAZYSUUOUUOUUCVIUVLYRUUENZY
      SUUOVJUUOYQMVKZUVMUUCABCDVLUVNUVCUUCYQMVMZUVHVNVOYRUUEVPVQVRVSUUCYSVTWAWB
      WCUUAMYTWDYTMNZOUUCYTWEYTMWFUUBUVPUUBYTMKMYRMWGWHWIUPWJZUUAUUMUUERZRZRZYR
      WKZUWAUVTUVSWKZUVSUVTWDZUWBOUVSUVSSUVRUUEUVAWLWLZWMZWNUWBUWCUVTSWDUVSSWDU
      WBUWCOVJUVSUWDWLUWDUVTUVSWOWPWQUHUWAYRUVSWKUWBABCDWRUVTYRUVSWSWTXAUUAUUHU
      UKUAZFSTESTUWAUUMUUAUWFUWAEFSSUUAUWFUWAVHUUFSWDUUJSWDUAUUAUWFUWAUUAUWFUAZ
      UVSYRWDZUWAUWGUVSYTRZYRUWGUVRYTWDZUVSUWIWDZUWGUUEYTWDZUWJUWGUUEUUGYTUUAUU
      HUUKXBUWGUUFYTWDZUUGYTWDZUWGUUGYTWKZUWMUWGUUGYTKZYTUUAUUHUUGUWPWKUUKUUAUU
      HUAUUGUUEUWPUUAUUHXCUUAUUEUWPWKZUUHUUAUUEUWPNUWQUUAYQKZKZHIZUUEUWPUUAUWSU
      UDHUUAUVNUWSUUDNUUAUUCUVNUVQUVNUVCUUCUVOUVHXDXEABXFVQXGUWTUWRHIZKUWPUWRXH
      UXAYTYQXHXIXJXKUUEUWPXLVQWBXMXNUUAYTUWPNUWFYTXOWBXPUUFXQWDYTUCZUWMUWOVJEX
      RUVIUXBUVKYRXSXTZUUFYTXQYAWPUOUUAUWMUWNVJUWFYTUUFYBWBYCYDUUAUWLUWJVJUWFYT
      UUEYBWBYCUXBUWJUWKVJUXCUVRYTYEXTXEUUKUWIYRNZUUAUUHYRSWDUUKUXDUVJYRUUJYFYG
      YHYIUVSXQWDUVIUWHUWAVJUWEUVKUVSYRXQYAWPXEVCYJYKUUHUUKEFSSYLYMYNYOYP $.
      $( [27-Sep-2006] $) $( [19-Sep-2006] $)

    $( The rank of a cross product when the rank of the union of its arguments
       is a successor ordinal.  Part of Exercise 4 of [Kunen] p. 107.  See
       ~ rankxplim for the limit ordinal case. $)
    rankxpsuc $p |- ( ( ( rank ` ( A u. B ) ) = suc C /\ ( A X. B ) =/= (/) )
                  -> ( rank ` ( A X. B ) ) = suc suc ( rank ` ( A u. B ) ) ) $=
      ( vx cun crnk cfv csuc wceq cxp c0 wne wa cuni unixp fveq2d rankuni
      unieqi eqtr syl5reqr con0 wcel wb rankon onuni ax-mp suc11 mp2an sylibr
      adantl cv wrex wlim wn wi cvv fvex eleq1 mpbii sucexb nlimsucg syl limeq
      mtbird rankxplim2 con3i wo df-ne xpex rankeq0 negbii bitr w3o word onord
      ordzsl mpbi 3orass ori sylbi ord con1d com12 3syl visset mtbiri a1i
      r19.23aiv rankxplim3 sylib syl6com unixp0 uniex eqeq1i 3bitr syld impcom
      onsucuni2 mpan eqtrd onsuc imp eqtr2d ) ABGZHIZCJZKZABLZMNZOZXGJZJZXJHIZP
      ZJZXOXLXMXPKZXNXQKZXLXMXPPZJZXPXKXMYAKZXIXKXGXTKZYBXKXJPZPZHIZXGXTXKYEXFH
      ABQRYFYDHIZPXTYDSYGXPXJSZTUAUBXGUCUDXTUCUDZYBYCUEXFUFZXPUCUDZYIXOUCUDZYKX
      JUFZXOUGUHZXPUGUHXGXTUIUJUKULXLXPFUMZJZKZFUCUNZYAXPKZXKXIYRXKXIXPUOZUPZYR
      XIXKXOYPKZFUCUNZUUAXIXGUOZUPXOUOZUPZXKUUCUQXIUUDXHUOZXICURUDZUUGUPXIXHURU
      DZUUHXIXGURUDUUIXFHUSXGXHURUTVACVBUKCURVCVDXGXHVEVFUUEUUDABDEVGVHXKUUFUUC
      XKUUCUUEXKUUCUUEXKXOMKZUPZUUCUUEVIZXKXJMKZUPZUUKXJMVJZUUMUUJXJABDEVKZVLVM
      VNUUJUULUUJUUCUUEVOZUUJUULVIXOVPUUQXOYMVQFXOVRVSUUJUUCUUEVTVSWAWBWCWDWEWF
      ZUUCUUFUUAUUBUUFFUCUUBUUFUQYOUCUDZUUBUUEYPUOZYOURUDUUTUPFWGYOURVCUHXOYPVE
      WHWIWJUUEYTABDEWKVMWLWMXKYRYTXKYRYTXKXPMKZUPZYRYTVIZXKUUNUVBUUOUUMUVAUUMY
      DMKYGMKUVAABWNYDXJUUPWOVLYGXPMYHWPWQVMVNUVAUVCUVAYRYTVOZUVAUVCVIXPVPUVDXP
      YNVQFXPVRVSUVAYRYTVTVSWAWBWCWDWRWSYQYSFUCYQYSUQUUSYKYQYSYNXPYOWTXAWIWJVDX
      BXMUCUDYKXSXRUEXGYJXCYNXMXPUIUJUKXLUUCXQXOKZXIXKUUCUURXDUUBUVEFUCUUBUVEUQ
      UUSYLUUBUVEYMXOYOWTXAWIWJVDXE $.
      $( [29-Sep-2006] $) $( [19-Sep-2006] $)
  $}

