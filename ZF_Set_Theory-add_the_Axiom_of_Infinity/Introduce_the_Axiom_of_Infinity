$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Introduce the Axiom of Infinity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
         +-------------------------------------------------+
         |         "Cantor's Paradise" begins here!        |
         | (i.e. we start to assume the Axiom of Infinity) |
         +-------------------------------------------------+
$)

  ${
    $d x y z w $.
    $( Axiom of Infinity.  An axiom of Zermelo-Fraenkel set theory.  This axiom
       is the gateway to "Cantor's paradise" (an expression coined by Hilbert).
       It asserts that given a starting set ` x ` , an infinite set ` y ` built
       from it exists.  Although our version is apparently not given in the
       literature, it is similar to, but slightly shorter than, the Axiom of
       Infinity in [FreydScedrov] p. 283 (see ~ inf1 and ~ inf2 ).  More
       standard versions, which essentially state that there exists a set
       containing all the natural numbers, are shown as ~ zfinf2 and ~ omex and
       are based on the (nontrivial) proof of ~ inf3 .  Our version has the
       advantage that when expanded to primitives, it has fewer symbols than
       the standard version ~ ax-inf2 .  Theorem ~ inf0 shows the reverse
       derivation of our axiom from a standard one.  Theorem ~ inf5 shows a
       very short way to state this axiom.

       An interesting property of our version is that, unlike the standard
       version, it does not assert the independent existence of any set; it
       needs a starting set ` x ` .  Since none of our other ZFC axioms assert
       the independent existence of any set, we would need to add an axiom of
       existence such as Axiom 0 of [Kunen] p. 10 if we were to use a "free
       logic" predicate calculus that (unlike ours) does not assert (as we do
       with ~ ax-4 and ~ ax-9 ) that at least one thing exists.

       The standard version of Infinity ~ ax-inf2 requires this axiom along
       with Regularity ~ ax-reg for its derivation (as theorem ~ axinf2
       below).  In order to more easily identify the normal uses of Regularity,
       we will usually reference ~ ax-inf2 instead of this one.  The derivation
       of this axiom from ~ ax-inf2 is shown by theorem ~ axinf . $)
    ax-inf $a |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $.

    $( Axiom of Infinity expressed with fewest number of different
       variables. $)
    zfinf $p |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( vw cv wcel wa wex wi wal ax-inf wceq elequ1 anbi1d exbidv imbi12d
      cbvalv anbi2i exbii mpbi ) BEZAEZFZDEZUBFZUDCEZFZUFUBFZGZCHZIZDJZGZAHUCUC
      UAUFFZUHGZCHZIZBJZGZAHBADCKUMUSAULURUCUKUQDBUDUALZUEUCUJUPDBAMUTUIUOCUTUG
      UNUHDBCMNOPQRST $.
      $( [14-Aug-2003] $)

    $( A standard version of Axiom of Infinity, expanded to primitives, derived
       from our version of Infinity ~ ax-inf and Regularity ~ ax-reg .

       This theorem should not be referenced in any proof.  Instead, use
       ~ ax-inf2 below so that the ordinary uses of Regularity can be more
       easily identified. $)
    axinf2 $p |- E. x ( E. y ( y e. x /\ A. z -. z e. y ) /\ A. y ( y e. x ->
          E. z ( z e. x /\ A. w ( w e. z <-> ( w e. y \/ w = y ) ) ) ) ) $=
      ( c0 cv wcel csuc wi wal wa wex wn wceq wo wb com peano1 peano2 ax-gen
      zfinf inf2 inf3 eleq2 imbi12d albidv anbi12d cla4ev mp2an wrex 0el
      df-rex bitri sucel imbi2i albii anbi12i exbii mpbi ) EAFZGZBFZUTGZVBHZUTG
      ZIZBJZKZALZVCCFZVBGMCJZKBLZVCVJUTGDFZVJGVMVBGVMVBNOPDJZKCLZIZBJZKZALEQGZV
      BQGZVDQGZIZBJZVIRWBBVBSTVHVSWCKAQAABCABCUAUBUCUTQNZVAVSVGWCUTQEUDWDVFWBBW
      DVCVTVEWAUTQVBUDUTQVDUDUEUFUGUHUIVHVRAVAVLVGVQVAVKBUTUJVLBCUTUKVKBUTULUMV
      FVPBVEVOVCVEVNCUTUJVOCDVBUTUNVNCUTULUMUOUPUQURUS $.
      $( [6-May-2008] $) $( [3-Nov-1996] $)

    $( A standard version of Axiom of Infinity of ZF set theory.  In English,
       it says:  there exists a set that contains the empty set and the
       successors of all of its members.  Theorem ~ zfinf2 shows it converted
       to abbreviations.  This axiom was derived as theorem ~ axinf2 above,
       using our version of Infinity ~ ax-inf and the Axiom of Regularity
       ~ ax-reg .  We will reference ~ ax-inf2 instead of ~ axinf2 so that the
       ordinary uses of Regularity can be more easily identified.  The reverse
       derivation of ~ ax-inf from ~ ax-inf2 is shown by theorem ~ axinf . $)
    ax-inf2 $a |- E. x ( E. y ( y e. x /\ A. z -. z e. y ) /\ A. y ( y e. x ->
          E. z ( z e. x /\ A. w ( w e. z <-> ( w e. y \/ w = y ) ) ) ) ) $.
  $}

  ${
    $d x y z w $.
    $( A standard version of the Axiom of Infinity, using definitions to
       abbreviate.  Axiom Inf of [BellMachover] p. 472.  (See ~ ax-inf2 for the
       unabbreviated version.) $)
    zfinf2 $p |- E. x ( (/) e. x /\ A. y e. x suc y e. x ) $=
      ( vz vw c0 cv wcel csuc wral wa wex wn wal wceq wo wb wi ax-inf2 wrex
      0el df-rex bitri sucel ralbii df-ral anbi12i exbii mpbir ) EAFZGZBFZHUIGZ
      BUIIZJZAKUKUIGZCFZUKGLCMZJBKZUOUPUIGDFZUPGUSUKGUSUKNOPDMZJCKZQBMZJZAKABCD
      RUNVCAUJURUMVBUJUQBUISURBCUITUQBUIUAUBUMVABUIIVBULVABUIULUTCUISVACDUKUIUC
      UTCUIUAUBUDVABUIUEUBUFUGUH $.
      $( [4-Mar-2004] $) $( [30-Aug-1993] $)
  $}

