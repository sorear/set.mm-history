$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Introduce the Axiom of Infinity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
         +-------------------------------------------------+
         |         "Cantor's Paradise" begins here!        |
         | (i.e. we start to assume the Axiom of Infinity) |
         +-------------------------------------------------+
$)

  ${
    $d x y z w $.
    $( Axiom of Infinity.  An axiom of Zermelo-Fraenkel set theory.  This axiom
       is the gateway to "Cantor's paradise" (an expression coined by Hilbert).
       It asserts that given a starting set ` x ` , an infinite set ` y ` built
       from it exists.  Although our version is apparently not given in the
       literature, it is similar to, but slightly shorter than, the Axiom of
       Infinity in [FreydScedrov] p. 283 (see ~ inf1 and ~ inf2 ).  More
       standard versions, which essentially state that there exists a set
       containing all the natural numbers, are shown as ~ zfinf and ~ omex and
       are based on the (nontrivial) proof of ~ inf3 .  Our version has the
       advantage that when expanded to primitives, it has fewer symbols than
       the expanded standard version ~ inf4 .  Theorem ~ inf0 shows the reverse
       derivation of our axiom from a standard one.  Theorem ~ inf5 shows a
       very short way to state this axiom.

       An interesting property of our version is that, unlike the standard
       version, it does not assert the independent existence of any set; it
       needs a starting set ` x ` .  Since none of our other ZFC axioms assert
       the independent existence of any set, we would need to add an axiom of
       existence such as Axiom 0 of [Kunen] p. 10 if we were to use a free
       predicate calculus that (unlike ours) does not assert that at least one
       thing exists.  $)
    ax-inf $a |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $.

    $( Axiom of Infinity expressed with fewest number of different
       variables.  $)
    axinf $p |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( vw cv wcel wa wex wi wal ax-inf wceq elequ1 anbi1d exbidv imbi12d
      cbvalv anbi2i exbii mpbi ) BEZAEZFZDEZUBFZUDCEZFZUFUBFZGZCHZIZDJZGZAHUCUC
      UAUFFZUHGZCHZIZBJZGZAHBADCKUMUSAULURUCUKUQDBUDUALZUEUCUJUPDBAMUTUIUOCUTUG
      UNUHDBCMNOPQRST $.
      $( [14-Aug-2003] $)

    $( A standard version of Axiom of Infinity, expanded to primitives.  In
       English, it says:  there exists a set that contains the empty set and
       the successors of all of its members.  See ~ zfinf to see it converted
       to abbreviations. $)
    inf4 $p |- E. x ( E. y ( y e. x /\ A. z -. z e. y ) /\ A. y ( y e. x ->
          E. z ( z e. x /\ A. w ( w e. z <-> ( w e. y \/ w = y ) ) ) ) ) $=
      ( c0 cv wcel csuc wi wal wa wex wn wceq wo wb com peano1 peano2 ax-gen
      axinf inf2 inf3 eleq2 imbi12d albidv anbi12d cla4ev mp2an wrex 0el
      df-rex bitr sucel imbi2i albii anbi12i exbii mpbi ) EAFZGZBFZUTGZVBHZUTGZ
      IZBJZKZALZVCCFZVBGMCJZKBLZVCVJUTGDFZVJGVMVBGVMVBNOPDJZKCLZIZBJZKZALEQGZVB
      QGZVDQGZIZBJZVIRWBBVBSTVHVSWCKAQAABCABCUAUBUCUTQNZVAVSVGWCUTQEUDWDVFWBBWD
      VCVTVEWAUTQVBUDUTQVDUDUEUFUGUHUIVHVRAVAVLVGVQVAVKBUTUJVLBCUTUKVKBUTULUMVF
      VPBVEVOVCVEVNCUTUJVOCDVBUTUNVNCUTULUMUOUPUQURUS $.
      $( [3-Nov-1996] $)
  $}

  ${
    $d x y z w $.
    $( A standard version of the Axiom of Infinity, using definitions to
       abbreviate.  Axiom Inf of [BellMachover] p. 472.  (See ~ inf4 for the
       unabbreviated version.) $)
    zfinf $p |- E. x ( (/) e. x /\ A. y e. x suc y e. x ) $=
      ( vz vw c0 cv wcel csuc wral wa wex wn wal wceq wo wb wi inf4 wrex 0el
      df-rex bitr sucel ralbii df-ral anbi12i exbii mpbir ) EAFZGZBFZHUIGZBUIIZ
      JZAKUKUIGZCFZUKGLCMZJBKZUOUPUIGDFZUPGUSUKGUSUKNOPDMZJCKZQBMZJZAKABCDRUNVC
      AUJURUMVBUJUQBUISURBCUITUQBUIUAUBUMVABUIIVBULVABUIULUTCUISVACDUKUIUCUTCUI
      UAUBUDVABUIUEUBUFUGUH $.
      $( [4-Mar-2004] $) $( [30-Aug-1993] $)
  $}

