$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Scott's trick; collection principle; Hilbert's epsilon
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, is a set. $)
    scottex $p |- { x e. A | A. y e. A ( rank ` x ) C_ ( rank ` y ) } e. _V $=
      ( vz vw c0 wceq cv crnk cfv wss wral crab cvv wcel 0ex eleq1 mpbiri
      rabexg syl wn wex neq0 hbra1 ax-17 hbrab hbel wi ra4 com12 a1d r19.21aiv
      ss2rab sylibr con0 wrex rankon fveq2 sseq1d elrab simprbi rgen sseq2
      ralbidv rcla4ev mp2an bndrank ax-mp ssex 19.23ai sylbi pm2.61i ) CFGZAHZI
      JZBHZIJZKZBCLZACMZNOZVMCNOZWAVMWBFNOPCFNQRVSACNSTVMUAVPCOZBUBWABCUCWCWABB
      DDVTNVSBADCVRBCUDDHZCOBUEUFWDNOBUEUGWCVTVRACMZKZWAWCVSVRUHZACLWFWCWGACWCW
      GVNCOVSWCVRVRBCUIUJUKULVSVRACUMUNVTWEEHZIJZWDKZEWELZDUOUPZWENOVQUOOWIVQKZ
      EWELZWLVPUQWMEWEWHWEOWHCOWMVRWMAWHCVNWHGVOWIVQVNWHIURUSUTVAVBWKWNDVQUOWDV
      QGWJWMEWEWDVQWIVCVDVEVFDEWEVGVHVITVJVKVL $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, contains at
       least one representative with the property, if there is one.  In other
       words, the collection is empty iff no set has the property (i.e. ` A `
       is empty). $)
    scott0 $p |- ( A = (/) <->
               { x e. A | A. y e. A ( rank ` x ) C_ ( rank ` y ) } = (/) ) $=
      ( vz c0 wceq cv crnk cfv wss wral crab rabeq rab0 syl6eq wne wrex ciin
      cab wcel wex n0 hbre1 eqid ra4e mpan2 19.23ai sylbi wa fvex eqeq1 anbi2d
      cla4ev eximi excom sylibr df-rex exbii 3imtr4i syl abn0 cint con0 wi
      hbab1 ax-17 dfss2f abid rankon eleq1 mpbiri r19.23aivw mpgbir onint mpan
      dfiin2 syl5eqel hbii1 hbeleq rexbid elabg ibi sseq1 ssid fveq2 sseq1d
      rcla4ev iinss syl5bi r19.21aiv reximi 3syl rabn0 necon4i impbii ) CEFZAGZ
      HIZBGZHIZJZBCKZACLZEFWPXCXBAELEXBACEMXBANOCEXCECEPZXBACQZXCEPXDACWRRZWSWR
      FZACQZBSZTZXFWRFZACQZXEXDXIEPZXJXDXHBUAZXMXDWRWRFZACQZXNXDWQCTZAUAXPACUBX
      QXPAXOACUCXQXOXPWRUDXOACUEUFUGUHXQXOUIZAUAZXQXGUIZAUAZBUAZXPXNXSXTBUAZAUA
      YBXRYCAXTXRBWRWQHUJZXGXGXOXQWSWRWRUKULUMUNXTBAUOUPXOACUQXHYABXGACUQURUSUT
      XHBVAUPXMXIVBZXIXFXIVCJZXMYEXITYFWSXITZWSVCTZVDBBDXIVCXHBDVEDGVCTBVFVGYGX
      HYHXHBVHXGYHACXGYHWRVCTWQVIWSWRVCVJVKVLUHVMXIVNVOABCWRYDVPVQUTXJXLXHXLBXF
      XIWSXFFXGXKACABXFABCWRVRVSWSXFWRUKVTWAWBXKXBACXKXABCXKXFWTJZXAWSCTZXFWRWT
      WCYJXAACQZYIYJWTWTJZYKWTWDXAYLAWSCWQWSFWRWTWTWQWSHWEWFWGUFACWRWTWHUTWIWJW
      KWLXBACWMUPWNWO $.
      $( [15-Oct-2003] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( Theorem scheme version of ~ scottex .  The collection of all ` x ` of
       minimum rank such that ` ph ( x ) ` is true, is a set. $)
    scottexs $p |- { x | ( ph /\
             A. y ( [ y / x ] ph -> ( rank ` x ) C_ ( rank ` y ) ) ) } e. _V $=
      ( vz cv crnk cfv wss cab wral crab wsbc wi wal wa cvv wcel ax-17 hbab1
      hbral wceq fveq2 sseq1d ralbidv cbvrab df-rab abid df-ral df-clab imbi1i
      albii bitri anbi12i abbii 3eqtri scottex eqeltrri ) DEZFGZCEZFGZHZCABIZJZ
      DVCKZAABUTLZBEZFGZVAHZMZCNZOZBIZPVEVICVCJZBVCKVGVCQZVNOZBIVMVDVNDBCVCUTVC
      QZDRABCSZVBBCVCVRVBBRTVNDRURVGUAZVBVICVCVSUSVHVAURVGFUBUCUDUEVNBVCUFVPVLB
      VOAVNVKABUGVNVQVIMZCNVKVICVCUHVTVJCVQVFVIACBUIUJUKULUMUNUODCVCUPUQ $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( Theorem scheme version of ~ scott0 .  The collection of all ` x ` of
       minimum rank such that ` ph ( x ) ` is true, is not empty iff there is
       an ` x ` such that ` ph ( x ) ` holds. $)
    scott0s $p |- ( E. x ph <-> { x | ( ph /\
         A. y ( [ y / x ] ph -> ( rank ` x ) C_ ( rank ` y ) ) ) } =/= (/) ) $=
      ( vz wex cab c0 wne cv wsbc crnk cfv wss wi wal wa abn0 wceq wral crab
      scott0 wcel ax-17 hbab1 hbral fveq2 sseq1d ralbidv cbvrab df-rab abid
      df-ral df-clab imbi1i albii bitri anbi12i abbii 3eqtri eqeq1i necon3bii
      bitr3i ) ABEABFZGHAABCIZJZBIZKLZVDKLZMZNZCOZPZBFZGHABQVCGVMGVCGRDIZKLZVHM
      ZCVCSZDVCTZGRVMGRDCVCUAVRVMGVRVICVCSZBVCTVFVCUBZVSPZBFVMVQVSDBCVCVDVCUBZD
      UCABCUDZVPBCVCWCVPBUCUEVSDUCVNVFRZVPVICVCWDVOVGVHVNVFKUFUGUHUIVSBVCUJWAVL
      BVTAVSVKABUKVSWBVINZCOVKVICVCULWEVJCWBVEVIACBUMUNUOUPUQURUSUTUPVAVB $.
      $( [6-Apr-2007] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d w C $.  $d w D $.
    cplem1.1 $e |- C = { y e. B | A. z e. B ( rank ` y ) C_ ( rank ` z ) } $.
    cplem1.2 $e |- D = U_ x e. A C $.
    $( Lemma for the Collection Principle ~ cp . $)
    cplem1 $p |- A. x e. A ( B =/= (/) -> ( B i^i D ) =/= (/) ) $=
      ( vw c0 wne cin wi cv wcel wex wa crnk cfv wss wral crab ssrab2 eqsstri
      sseli a1i ciun ssiun2 syl6ssr sseld jcad inelcm syl6 19.23adv wceq
      scott0 eqeq1i bitr4i necon3bii n0 bitri syl5ib rgen ) EKLZEGMKLZNADAODPZJ
      OZFPZJQZVFVEVGVIVFJVGVIVHEPZVHGPZRVFVGVIVKVLVIVKNVGFEVHFBOSTCOSTUACEUBZBE
      UCZEHVMBEUDUEUFUGVGFGVHVGFADFUHGADFUIIUJUKULVHEGUMUNUOVEFKLVJEKFKEKUPVNKU
      PFKUPBCEUQFVNKHURUSUTJFVAVBVCVD $.
      $( [2-Apr-2007] $) $( [17-Oct-2003] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.
    cplem2.1 $e |- A e. _V $.
    $( -Lemma for the Collection Principle ~ cp . $)
    cplem2 $p |- E. y A. x e. A ( B =/= (/) -> ( B i^i y ) =/= (/) ) $=
      ( vz vw c0 wne cv crnk cfv wss wral crab ciun cin wi wex eqid cplem1
      scottex iunex wceq hbiu1 hbeleq ineq2 neeq1d imbi2d ralbid cla4ev ax-mp
      ) DHIZDACFJKLGJKLMGDNFDOZPZQZHIZRZACNZUMDBJZQZHIZRZACNZBSAFGCDUNUOUNTUOTU
      AVDUSBUOACUNEFGDUBUCUTUOUDZVCURACABUOABCUNUEUFVEVBUQUMVEVAUPHUTUODUGUHUIU
      JUKUL $.
      $( [2-Apr-2007] $) $( [17-Oct-2003] $)
  $}

  ${
    $d ph z w $.  $d x y z w $.
    $( Collection Principle.  This remarkable theorem scheme is in effect a
       very strong generalization of the Axiom of Replacement.  The proof makes
       use of Scott's trick ~ scottex that collapses a proper class into a set
       of minimum rank.  The wff ` ph ` can be thought of as
       ` ph ( x , y ) ` .  Scheme "Collection Principle" of [Jech] p. 72. $)
    cp $p |- E. w A. x e. z ( E. y ph -> E. y e. w ph ) $=
      ( cab c0 wne cv cin wi wral wex wrex visset cplem2 abn0 wcel wa elin
      abid anbi1i ancom 3bitri exbii hbab1 ax-17 hbin ne0f df-rex 3bitr4i
      imbi12i ralbii mpbi ) ACFZGHZUOEIZJZGHZKZBDIZLZEMACMZACUQNZKZBVALZEMBEVAU
      ODOPVBVFEUTVEBVAUPVCUSVDACQCIZURRZCMVGUQRZASZCMUSVDVHVJCVHVGUORZVISAVISVJ
      VGUOUQTVKAVIACUAUBAVIUCUDUECDURCDUOUQACDUFVAUQRCUGUHUIACUQUJUKULUMUEUN $.
      $( [17-Oct-2003] $)
  $}

  ${
    $d ph z w $.  $d x y z w $.
    $( A very strong generalization of the Axiom of Replacement (compare
       ~ zfrep6 ), derived from the Collection Principle ~ cp .  Its strength
       lies in the rather profound fact that ` ph ( x , y ) ` does not have to
       be a "function-like" wff, as it does in the standard Axiom of
       Replacement.  This theorem is sometimes called the Boundedness Axiom. $)
    bnd $p |- ( A. x e. z E. y ph -> E. w A. x e. z E. y e. w ph ) $=
      ( wex cv wral wrex wi cp ralim eximi ax-mp 19.37v mpbi ) ACFZBDGZHZACEGIZ
      BRHZJZEFZSUAEFJQTJBRHZEFUCABCDEKUDUBEQTBRLMNSUAEOP $.
      $( [2-Jan-2004] $) $( [17-Oct-2004] $)
  $}

  ${
    $d ph z w v $.  $d x z w v A $.  $d x y z w v B $.
    bnd2.1 $e |- A e. _V $.
    $( A variant of the Boundedness Axiom ~ bnd that picks a subset ` z ` out
       of a possibly proper class ` B ` in which a property is true. $)
    bnd2 $p |- ( A. x e. A E. y e. B ph ->
              E. z ( z C_ B /\ A. x e. A E. y e. z ph ) ) $=
      ( vw vv wrex wral cv wcel wa wex wss df-rex ralbii wi wceq raleq exbidv
      imbi12d bnd vtocl sylbi cin visset inex1 inss2 sseq1 mpbiri biantrurd
      rexeq elin anbi1i anass bitri rexbii2 syl6bb ralbidv bitr3d cla4ev
      19.23aiv syl ) ACFJZBEKZCLZFMZANZCHLZJZBEKZHOZDLZFPZACVOJZBEKZNZDOZVGVJCO
      ZBEKZVNVFWABEACFQRWABILZKZVLBWCKZHOZSWBVNSIEGWCETZWDWBWFVNWABWCEUAWGWEVMH
      VLBWCEUAUBUCVJBCIHUDUEUFVMVTHVSVMDVKFUGZVKFHUHUIVOWHTZVRVSVMWIVPVRWIVPWHF
      PVKFUJVOWHFUKULUMWIVQVLBEWIVQACWHJVLACVOWHUNAVJCWHVKVHWHMZANVHVKMZVINZANW
      KVJNWJWLAVHVKFUOUPWKVIAUQURUSUTVAVBVCVDVE $.
      $( [4-Feb-2004] $)
  $}

  ${
    $d x y z A $.
    $( The collection of all sets equinumerous to a set ` A ` and having least
       possible rank is a set.  This is the part of the justification of the
       definition of kard of [Enderton] p. 222. $)
    kardex $p |- { x | ( x ~~ A /\
                  A. y ( y ~~ A -> ( rank ` x ) C_ ( rank ` y ) ) ) } e. _V $=
      ( vz cv crnk cfv wss cen wbr cab wral crab wi wal wa cvv wcel df-rab
      visset breq1 elab ralab anbi12i abbii eqtri scottex eqeltrri ) AEZFGBEZFG
      HZBDEZCIJZDKZLZAUNMZUICIJZUJCIJZUKNBOZPZAKZQUPUIUNRZUOPZAKVAUOAUNSVCUTAVB
      UQUOUSUMUQDUIATULUICIUAUBUMURUKBDULUJCIUAUCUDUEUFABUNUGUH $.
      $( [21-Mar-2007] $) $( [14-Dec-2003] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d z C $.  $d z D $.
    karden.1 $e |- A e. _V $.
    karden.2 $e |- B e. _V $.
    karden.3 $e |- C =
       { x | ( x ~~ A /\ A. y ( y ~~ A -> ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    karden.4 $e |- D =
       { x | ( x ~~ B /\ A. y ( y ~~ B -> ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    $( If we allow the Axiom of Regularity, we can avoid the Axiom of Choice by
       defining the cardinal number of a set as the set of all sets
       equinumerous to it and having least possible rank.  This theorem proves
       the equinumerosity relationship for this definition (compare
       ~ carden ).  The hypotheses correspond to the definition of kard of
       [Enderton] p. 222 (which we don't define separately since currently we
       do not use it elsewhere).  This theorem along with ~ kardex justify the
       definition of kard.  The restriction to least rank prevents the proper
       class that would result from ` { x | x ~~ A } ` . $)
    karden $p |- ( C = D <-> A ~~ B ) $=
      ( vz vw wceq cen wbr cv crnk cfv wss cab wral wrex crab c0 wne wex enref
      breq1 cla4ev ax-mp abn0 mpbir scott0 necon3bii mpbi rabn0 wcel wi wal wa
      simpl a1i wb eqeq12i eq2ab bitri fveq2 sseq1d imbi2d albidv anbi12d
      bibi12d a4v sylbi syl6bi jcad ensym entr sylan syl6 visset elab ralab
      anbi12i syl5ib exp3a r19.23adv mpi cvv enen2 mpan imbi1d abbidv 3eqtr4g
      impbii ) EFMZCDNOZWPKPZQRZBPZQRZSZBLPZCNOZLTZUAZKXEUBZWQXFKXEUCZUDUEZXGXE
      UDUEZXIXJXDLUFZCCNOZXKCGUGXDXLLCGXCCCNUHUIUJXDLUKULXEUDXHUDKBXEUMUNUOXFKX
      EUPUOWPXFWQKXEWPWRXEUQZXFWQWPWRCNOZWTCNOZXBURZBUSZUTZWQXMXFUTWPXRXNWRDNOZ
      UTWQWPXRXNXSXRXNURWPXNXQVAVBWPXRXSWTDNOZXBURZBUSZUTZXSWPAPZCNOZXOYDQRZXAS
      ZURZBUSZUTZYDDNOZXTYGURZBUSZUTZVCZAUSZXRYCVCZWPYJATZYNATZMYPEYRFYSIJVDYJY
      NAVEVFYOYQAKYDWRMZYJXRYNYCYTYEXNYIXQYDWRCNUHYTYHXPBYTYGXBXOYTYFWSXAYDWRQV
      GVHZVIVJVKYTYKXSYMYBYDWRDNUHYTYLYABYTYGXBXTUUAVIVJVKVLVMVNXSYBVAVOVPXNCWR
      NOXSWQWRCGVQCWRDVRVSVTXMXNXFXQXDXNLWRKWAXCWRCNUHWBXDXOXBBLXCWTCNUHWCWDWEW
      FWGWHWQYRYSEFWQYJYNAWQYEYKYIYMDWIUQZWQYEYKVCHCDYDWIWJWKWQYHYLBWQXOXTYGUUB
      WQXOXTVCHCDWTWIWJWKWLVJVKWMIJWNWO $.
      $( [18-Dec-2003] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    htalem.1 $e |- A e. _V $.
    htalem.2 $e |- B = U. { x e. A | A. y e. A -. y R x } $.
    $( Lemma for defining an emulation of Hilbert's epsilon.  Hilbert's epsilon
       is described at ~ http://plato.stanford.edu/entries/epsilon-calculus/ .
       This theorem is equivalent to Hilbert's "transfinite axiom," described
       on that page, with the additional ` R We A ` antecedent.  The element
       ` B ` is the epsilon that the theorem emulates. $)
    htalem $p |- ( ( R We A /\ A =/= (/) ) -> B e. A ) $=
      ( wwe c0 wne wa cv wbr wn wral crab cuni wss wcel ssid wereucl mp3an2
      syl5eqel ) CEHZCIJZKBLALEMNBCOACPQZCDUDCCRUEUFCSCTABCCEFUAUBGUC $.
      $( [10-Apr-2007] $) $( [11-Mar-2004] $)
  $}

  ${
    $d z v A $.  $d x y R z v $.  $d y z v ph $.
    hta.1 $e |- A = { x | ( ph /\ A. y ( [ y / x ] ph ->
                                         ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    hta.2 $e |- B = U. { x e. A | A. y e. A -. y R x } $.
    $( A ZFC emulation of Hilbert's transfinite axiom.  The set ` B ` has the
       properties of Hilbert's epsilon, except that it also depends on a
       well-ordering ` R ` .  This theorem arose from discussions with Raph
       Levien on 5-Mar-2004 about translating the HOL proof language, which
       uses Hilbert's epsilon.  See
       ~ http://us.metamath.org/downloads/choice.txt (copy of obsolete link
       http://ghilbert.org/choice.txt) and
       ~ http://us.metamath.org/downloads/megillaward2005he.pdf .

       Hilbert's epsilon is described at
       ~ http://plato.stanford.edu/entries/epsilon-calculus/ .  This theorem
       differs from Hilbert's transfinite axiom described on that page in that
       it requires ` R We A ` as an antecedent.  Class ` A ` collects the sets
       of least rank for which ` ph ( x ) ` is true.  Class ` B ` , which
       emulates the epsilon, is the minimum element in a well-ordering ` R ` on
       ` A ` .

       If a well-ordering ` R ` on ` A ` can be expressed in a closed form, as
       might be the case if we are working with say natural numbers, we can
       eliminate the antecedent with modus ponens, giving us the exact
       equivalent of Hilbert's transfinite axiom.  Otherwise, we replace ` R `
       with a dummy set variable, say ` w ` , and attach ` w We A ` as an
       antecedent in each step of the ZFC version of the HOL proof until the
       epsilon is eliminated.  At that point, ` B ` (which will have ` w ` as a
       free variable) will no longer be present, and we can eliminate
       ` w We A ` by applying ~ 19.23aiv and ~ weth , using ~ scottexs to
       establish the existence of ` A ` .

       For a version of this theorem scheme using class (meta)variables instead
       of wff (meta)variables, see ~ htalem . $)
    hta $p |- ( R We A -> ( ph -> [ B / x ] ph ) ) $=
      ( vz vv wwe cv wsbc crnk cfv wss wi wal wa cab c0 wne wcel wceq wb weeq2
      ax-mp scottexs wbr wn wral crab cuni ax-17 hba1 hban hbab hbxfr raleqf
      a1i rabbiia hbab1 rabeqf hbral breq2 notbid ralbidv cbvrab breq1 cbvralf
      eqtri 3eqtri unieqi htalem ex sylbi simpl ss2abi sseli cvv eqeltri hbel
      ssrab2 eqsstri ssexi uniex elabs sylib syl6 wex 19.8a scott0s syl5 ) DFKZ
      AABCLZMBLZNOWONOPQZCRZSZBTZUAUBZABEMZAWNXAEWTUCZXBWNWTFKZXAXCQDWTUDZWNXDU
      EGDWTFUFUGXDXAXCIJWTEFABCUHZEWOWPFUIZUJZCDUKZBDULZUMZJLZILZFUIZUJZJWTUKZI
      WTULZUMHXJXQXJXHCWTUKZBDULZXRBWTULZXQXIXRBDXIXRUEZWPDUCXEYAGXHCIDWTCIDWTG
      WSCBIAWRCACUNWQCUOUPUQZURYBUSUGUTVAXEXSXTUDGXRBIDWTBIDWTGWSBIVBZURZYCVCUG
      XTWOXMFUIZUJZCWTUKZIWTULXQXRYGBIJWTWSBJVBXLWTUCIUNXRIUNYFBCWTWSBCVBYFBUNZ
      VDWPXMUDZXHYFCWTYIXGYEWPXMWOFVEVFZVGVHYGXPIWTYGXPUEXMWTUCZYFXOCJIWTYBYKJU
      NYFJUNXOCUNWOXLUDYEXNWOXLXMFVIVFVJUTVAVKVLVMVKVNVOVPXCEABTZUCXBWTYLEWSABA
      WRVQVRVSABEEXKVTHXJXJDDWTVTGXFWAXJYFCDUKZIDULDXIYMBIJDBIIXLDXMXLUCBUNYDWB
      XLDUCIUNXIIUNYFBCDBIIWODXMWOUCBUNYDWBYHVDYIXHYFCDYJVGVHYMIDWCWDWEWFWAWGWH
      WIAABWJXAABWKABCWLWHWM $.
      $( [11-Mar-2004] $)

  $}

