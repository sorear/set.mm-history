$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Axiom of Choice equivalents
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u t h $.
    $( Equivalence of two versions of the Axiom of Choice ~ ax-ac .  The proof
       uses neither AC nor the Axiom of Regularity.  The right-hand side
       expresses our AC with the fewest number of different variables. $)
    aceq1 $p |- ( E. y A. z e. x A. w e. z E! v e. z E. u e. y
                      ( z e. u /\ v e. u ) <->
                  E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. x A. z ( E. x
           ( ( z e. w /\ w e. x ) /\ ( z e. x /\ x e. y ) ) <-> z = x ) ) ) $=
      ( vt vh cv wcel wa wrex wreu wral wex wceq wb wal wi biidd cbvralv eleq1
      anbi2d rexbidv cbvreuv ralbii bitri anbi1d reueqd raleqbi1dv 3bitr4i
      exbii 19.21v impexp bi2.04 albii weu df-eu df-reu 19.42v an42 anass
      bitr3i df-rex eleq2 anbi12d cbvexv anbi2i bibi1i imbi2i df-ral ax-17
      hba1 hbex hbim imbi1d cbval alcom 3bitr4ri ) CIZFIZJZEIZWAJZKZFBIZLZEVTMZ
      DVTNZCAIZNZBODIZWAJZWBKZFWFLZCWLMZGWLNZDWJNZBOVTWLJZWLWJJZKZXAVTWJJZWJWFJ
      ZKKZAOZVTWJPZQZCRZAOZSZDRCRZBOWKWRBHIZWAJZWDKZFWFLZEXLMZDXLNZHWJNXMWBKZFW
      FLZCXLMZGXLNZHWJNWKWRXQYAHWJXQXPGXLNYAXPXPDGXLWLGIZPXPTUAXPXTGXLXOXSECXLW
      CVTPZXNXRFWFYCWDWBXMWCVTWAUBUCUDUEUFUGUFWIXQCHWJWHXPDVTXLWGXOEVTXLVTXLPZW
      EXNFWFYDWBXMWDVTXLWAUBUHUDUIUJUAWQYADHWJWPXTGWLXLWOXSCWLXLWLXLPZWNXRFWFYE
      WMXMWBWLXLWAUBUHUDUIUJUAUKULWRXKBXJCRZDRWTWQSZDRXKWRYFYGDWTWSXISZSZCRWTYH
      CRZSYFYGWTYHCUMXJYICXJWSWTXISSYIWSWTXIUNWSWTXIUOUGUPWQYJWTYBWLJZWPSZGRYKX
      ISZGRWQYJYLYMGWPXIYKWSWOKZCUQYNXFQZCRZAOWPXIYNCAURWOCWLUSXHYPAXGYOCXEYNXF
      WSXCWTXBKZKZKZAOWSYRAOZKXEYNWSYRAUTXDYSAXDWSXCKYQKYSWSXCWTXBVAWSXCYQVBVCU
      LWOYTWSWOWAWFJZWNKZFOYTWNFWFVDUUBYRFAWAWJPZUUAXCWNYQWAWJWFUBUUCWMWTWBXBWA
      WJWLVEWAWJVTVEVFVFVGUGVHUKVIUPULUKVJUPWPGWLVKYHYMCGYHGVLYKXICYKCVLXHCAXGC
      VMVNVOVTYBPWSYKXIVTYBWLUBVPVQUKVJUKUPXJCDVRWQDWJVKVSULUG $.
      $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u t $.
    $( Equivalence of two versions of the Axiom of Choice.  The proof uses
       neither AC nor the Axiom of Regularity.  The right-hand side is our
       original ~ ax-ac . $)
    aceq0 $p |- ( E. y A. z e. x A. w e. z E! v e. z E. u e. y
                      ( z e. u /\ v e. u ) <->
                  E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u
     ( E. t ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) ) ) $=
      ( cv wcel wa wrex wreu wral wex wceq wb wal wi aceq1 equequ2 bibi2d
      elequ2 anbi2d elequ1 anbi12d cbvexv bibi1i syl6bb albidv anbi1d exbidv
      equequ1 bibi12d cbvalv imbi2i 2albii exbii bitr4i ) CHZFHZIEHZUTIJFBHZKEU
      SLDUSMCAHZMBNUSDHZIZVDVCIZJZVGUSVCIZVCVBIZJZJZANZUSVCOZPZCQZANZRZDQCQZBNV
      GUTVDIZVDGHZIZJZUTVTIZVTVBIZJZJZGNZUTVAOZPZFQZENZRZDQCQZBNABCDEFSWMVRBWLV
      QCDWKVPVGWJVOEAVAVCOZWJVSVFJZUTVCIZVIJZJZANZUTVCOZPZFQVOWNWIXAFWNWIWGWTPX
      AWNWHWTWGEAFTUAWGWSWTWFWRGAVTVCOZWBWOWEWQXBWAVFVSGADUBUCXBWCWPWDVIGAFUBGA
      BUDUEUEUFUGUHUIXAVNFCUTUSOZWSVLWTVMXCWRVKAXCWOVGWQVJXCVSVEVFFCDUDUJXCWPVH
      VIFCAUDUJUEUKFCAULUMUNUHUFUOUPUQUR $.
      $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u t $.
    $( Equivalence of two versions of the Axiom of Choice.  The proof uses
       neither AC nor the Axiom of Regularity. $)
    aceq2 $p |- ( E. y A. z e. x A. w e. z E! v e. z E. u e. y
                     ( z e. u /\ v e. u ) <->
                  E. y A. z e. x ( z =/= (/) -> E! w e. z E. v e. y
                      ( z e. v /\ w e. v ) ) ) $=
      ( vt cv wcel wa wrex wreu wral c0 wne wi wex wal df-ral 19.23v bitri
      wceq biidd cbvralv n0 eleq2 anbi12d cbvrexv reubii eleq1 anbi2d rexbidv
      cbvreuv imbi12i 3bitr4i ralbii exbii ) CHZFHZIZEHZUSIZJZFBHZKZEURLZDURMZC
      AHZMURNOZURVAIZDHZVAIZJZEVDKZDURLZPZCVHMBVGVPCVHVFGURMZGHZURIZGQZVFPZVGVP
      VQVSVFPGRWAVFGURSVSVFGTUAVFVFDGURVKVRUBVFUCUDVIVTVOVFGURUEVOUTVKUSIZJZFVD
      KZDURLVFVNWDDURVMWCEFVDVAUSUBVJUTVLWBVAUSURUFVAUSVKUFUGUHUIWDVEDEURVKVAUB
      ZWCVCFVDWEWBVBUTVKVAUSUJUKULUMUAUNUOUPUQ $.
      $( [7-Apr-2007] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u t h g f $.  $d t h g F $.
    aceq3lem.1 $e |- F =
              { <. w , v >. | ( w e. dom y /\ v = ( f ` { u | w y u } ) ) } $.
    $( Lemma for ~ aceq3 . $)
    aceq3lem $p |- ( A. x E. f A. z e. x ( z =/= (/) -> ( f ` z
                 ) e. z ) -> E. f ( f C_ y /\ f Fn dom y ) ) $=
      ( vg vt vh cv c0 wne cfv wcel wi wral wex wal wss cdm wfn wa crn cpw
      visset rnex pwex wceq raleq exbidv cla4v cvv wbr cab fvex fnopab2 dmex
      fnex mp2an sseq1 fneq1 anbi12d cla4ev wrel copab relopab releqi mpbir
      a1i cop eleq2i eleq1 breq1 abbidv fveq2d eqeq2d eqeq1 anbi2d opelopab
      bitri simplbi 19.8a ss2abi dfrn2 sseqtr4i elpw2 neeq1 fveq2 id eleq12d
      imbi12d rcla4v ax-mp eldm abn0 bitr4i syl5ib com12 fvopab4 eleq1d breq2
      cbvabv elab2 df-br bitr2i syl5bb sylibrd syl wb wfun fnfun funopfv opeq2
      sylibd relssdv sylancl 19.23aiv cbvexv sylib ) CMZNOZYCGMZPZYCQZRZCAMZSZG
      TZAUAZJMZBMZUBZYMYNUCZUDZUEZJTZYEYNUBZYEYPUDZUEZGTYLYHCYNUFZUGZSZGTZYSYKU
      UFAUUDUUCYNBUHZUIZUJYIUUDUKYJUUEGYHCYIUUDULUMUNUUEYSGHYNUBZHYPUDZYSUUEYRU
      UIUUJUEJHUUJYPUOQHUOQDEYPDMZFMZYNUPZFUQZYEPZHUUNYEURIUSZYNUUGUTYPUOHVAVBY
      MHUKYOUUIYQUUJYMHYNVCYPYMHVDVEVFUUEKLHYNHVGZUUEUUQUUKYPQZEMZUUOUKZUEZDEVH
      ZVGUVADEVIHUVBIVJVKVLKMZLMZVMZHQZUUEUVEYNQZUVFUUEUVCUVCHPZVMZYNQZUVGUVFUV
      CYPQZUUEUVJRUVFUVKUVDUVCUULYNUPZFUQZYEPZUKZUVFUVEUVBQUVKUVOUEZHUVBUVEIVNU
      VAUVKUUSUVNUKZUEUVPDEUVCUVDKUHZLUHZUUKUVCUKZUURUVKUUTUVQUUKUVCYPVOUVTUUOU
      VNUUSUVTUUNUVMYEUVTUUMUVLFUUKUVCUULYNVPVQVRZVSVEUUSUVDUKUVQUVOUVKUUSUVDUV
      NVTWAWBWCWDUVKUUEUVNUVMQZUVJUUEUVKUWBUUEUVMNOZUWBUVKUVMUUDQZUUEUWCUWBRZRU
      WDUVMUUCUBUVMUVLKTZFUQUUCUVLUWFFUVLKWEWFKFYNWGWHUVMUUCUUHWIVKYHUWECUVMUUD
      YCUVMUKZYDUWCYGUWBYCUVMNWJUWGYFUVNYCUVMYCUVMYEWKUWGWLWMWNWOWPUVKUVLFTUWCF
      UVCYNUVRWQUVLFWRWSWTXAUVKUVHUVMQZUWBUVJUVKUVHUVNUVMDEUVCUUOUVNYPHUWAIUVMY
      EURXBXCUWHUVCUVHYNUPZUVJUVCUVDYNUPZUWILUVHUVMUVCHURUVDUVHUVCYNXDUVLUWJFLU
      ULUVDUVCYNXDXEXFUVCUVHYNXGXHXIXJXKUVFUVHUVDUKZUVJUVGXLHXMZUVFUWKRUUJUWLUU
      PYPHXNWPUVCUVDHUVSXOWPUWKUVIUVEYNUVHUVDUVCXPXCXKXQXAXRUUPXSXTXKYRUUBJGYMY
      EUKYOYTYQUUAYMYEYNVCYPYMYEVDVEYAYB $.
      $( [14-Apr-2007] $) $( [2-Apr-2004] $)
  $}

  ${
    $d f x y z w v u g h $.
    $( Equivalence of two versions of the Axiom of Choice.  The left-hand side
       is similar to the Axiom of Choice (first form) of [Enderton] p. 49.  The
       right-hand side is the Axiom of Choice of [TakeutiZaring] p. 83.  The
       proof does not depend on AC. $)
    aceq3 $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
             A. x E. f A. z e. x ( z =/= (/) -> ( f ` z ) e. z ) ) $=
      ( vy vw vv vu cv wss cdm wfn wa wex wal c0 wne cfv wcel wi wral wceq
      sseq2 dmeq fneq2d anbi12d exbidv cbvalv copab cuni cxp visset uniex xpex
      simpl elunii ancoms jca ssopab2i df-xp sseqtr4i ssexi cla4v csn cima wb
      fndm eleq2 cab dmopab eleq2i 19.42v eleq1 elab n0 anbi2i 3bitr4i bitr2i
      syl6rbbr syl adantl wfun funfvima3 fnfun sylan2 sylbid imp ibar abbi2dv
      wbr cvv imasng ax-mp anbi2d eqid brab abbii eqtri syl6reqr eleq2d
      ad2antrl mpbid exp32 r19.21aiv eximi 19.21aiv aceq3lem impbii bitri ) CHZ
      AHZIZXIXJJZKZLZCMZANXIDHZIZXIXPJZKZLZCMZDNZBHZOPZYCXIQZYCRZSZBXJTZCMZANZX
      OYAADXJXPUAZXNXTCYKXKXQXMXSXJXPXIUBYKXLXRXIXJXPUCUDUEUFUGYBYJYBYIAYBXIEHZ
      XJRZFHZYLRZLZEFUHZIZXIYQJZKZLZCMZYIYAUUBDYQYQXJXJUIZUJZXJUUCAUKZXJUUEULUM
      YQYMYNUUCRZLZEFUHUUDYPUUGEFYPYMUUFYMYOUNYOYMUUFYNYLXJUOUPUQUREFXJUUCUSUTV
      AXPYQUAZXTUUACUUHXQYRXSYTXPYQXIUBUUHXRYSXIXPYQUCUDUEUFVBUUAYHCUUAYGBXJUUA
      YCXJRZYDYFUUAUUIYDLZLYEYQYCVCVDZRZYFUUAUUJUULUUAUUJYCXIJZRZUULYTUUJUUNVEZ
      YRYTUUMYSUAZUUOYSXIVFUUPUUNYCYSRZUUJUUMYSYCVGUUQYCYPFMZEVHZRZUUJYSUUSYCYP
      EFVIVJUUIYNYCRZLZFMZUUIUVAFMZLUUTUUJUUIUVAFVKUURUVCEYCBUKZYLYCUAZYPUVBFUV
      FYMUUIYOUVAYLYCXJVLYLYCYNVGUEZUFVMYDUVDUUIFYCVNVOVPVQVRVSVTYRXIWAZUUNUULS
      ZYTUVHYRUVIYCXIYQWBUPYSXIWCWDWEWFUUIUULYFVEUUAYDUUIUUKYCYEUUIYCUUIGHZYCRZ
      LZGVHZUUKUUIUVLGYCUUIUVKWGWHUUKYCUVJYQWIZGVHZUVMYCWJRUUKUVOUAUVEGYCWJYQWK
      WLUVNUVLGYPUVBUVLEFYCUVJYQUVEGUKUVGYNUVJUAUVAUVKUUIYNUVJYCVLWMYQWNWOWPWQW
      RWSWTXAXBXCXDVSXEYJYADADBEFGCYLXRRYNYLUVJXPWIGVHXIQUALEFUHZUVPWNXFXEXGXH
      $.
      $( [14-Apr-2007] $) $( [24-Mar-2004] $)
  $}

  ${
    $d f x z y w v $.
    $( Equivalence of two versions of the Axiom of Choice.  The left-hand side
       is similar to the Axiom of Choice (first form) of [Enderton] p. 49.  The
       right-hand side is Axiom AC of [BellMachover] p. 488.  The proof does
       not depend on AC. $)
    aceq4 $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
        A. x E. f ( f Fn x /\ A. z e. x ( z =/= (/) -> ( f ` z ) e. z ) ) ) $=
      ( vy vw vv cv wss cdm wfn wa wex wal c0 wne cfv wcel wi wral aceq3 wceq
      fveq1 eleq1d imbi2d ralbidv cbvexv copab fveq2 eqid fvex fvopab4 ralbiia
      anbi2i fnopab2 mpbiran visset opabex2 fneq1 anbi12d cla4ev sylbir
      19.23aiv sylbi simpr eximi impbii albii bitri ) CGZAGZHVIVJIJKCLAMBGZNOZV
      KVIPZVKQZRZBVJSZCLZAMVIVJJZVPKZCLZAMABCTVQVTAVQVTVQVLVKDGZPZVKQZRZBVJSZDL
      VTVPWECDVIWAUAZVOWDBVJWFVNWCVLWFVMWBVKVKVIWAUBUCUDUEUFWEVTDWEEGZVJQFGWGWA
      PZUAKEFUGZVJJZVLVKWIPZVKQZRZBVJSZKZVTWOWJWEWNWEWJWMWDBVJVKVJQZWLWCVLWPWKW
      BVKEFVKWHWBVJWIWGVKWAUHWIUIZVKWAUJUKUCUDULUMEFVJWHWIWGWAUJWQUNUOVSWOCWIEF
      VJWHAUPUQVIWIUAZVRWJVPWNVJVIWIURWRVOWMBVJWRVNWLVLWRVMWKVKVKVIWIUBUCUDUEUS
      UTVAVBVCVSVPCVRVPVDVEVFVGVH $.
      $( [17-Apr-2007] $) $( [24-Mar-2004] $)
  $}

  ${
    $d v w y g t $.
    $( Lemma for ~ aceq5 . $)
    aceq5lem1 $p |- ( E! v v e. ( ( { w } X. w ) i^i y ) <->
                      E! g ( g e. w /\ <. w , g >. e. y ) ) $=
      ( vt cv csn cxp cin wcel weu cop wceq wa wex elin elxp excom bitri
      anbi1i 19.41vv an32 eleq1 pm5.32i elsn anbi12i an4 ancom anass 3bitri
      exbii visset opeq1 eqeq2d eleq1d anbi2d anbi12d ceqsexv bitr3i eubii
      euop2 ) CFZBFZGZVCHZAFZIJZCKVBVCDFZLZMZVHVCJZVIVFJZNZNZDOZCKVMDKVGVOCVGVB
      VEJZVBVFJZNVBEFZVHLZMZVRVDJZVKNZNZEODOZVQNZVOVBVEVFPVPWDVQVPWCDOEOWDEDVBV
      DVCQWCEDRSTWEWCVQNZEOZDOVOWCVQDEUAWGVNDWGVRVCMZVTVKVSVFJZNZNZNZEOVNWFWLEW
      FVTVQNZWBNVTWINZWHVKNZNZWLVTWBVQUBWMWNWBWOVTVQWIVBVSVFUCUDWAWHVKEVCUETUFW
      PVTWHNZWIVKNZNWHVTNZWJNWLVTWIWHVKUGWQWSWRWJVTWHUHWIVKUHUFWHVTWJUIUJUJUKWK
      VNEVCBULWHVTVJWJVMWHVSVIVBVRVCVHUMZUNWHWIVLVKWHVSVIVFWTUOUPUQURSUKUSUJUTV
      MCDVCVAS $.
      $( [12-Apr-2004] $)
  $}

  ${
    $d f x z y w v u t h g $.  $d z w f g B $.  $d x y z w g A $.
    aceq5lem.1 $e |- A =
                    { u | ( u =/= (/) /\ E. t e. h u = ( { t } X. t ) ) } $.
    $( Lemma for ~ aceq5 . $)
    aceq5lem2 $p |- ( <. w , g >. e. U. A <-> ( w e. h /\ g e. w ) ) $=
      ( cv cop cuni wcel c0 wne csn cxp wceq wrex wa cab wex unieqi eleq2i
      eluniab r19.42v anass bitr2i exbii rexcom4 df-rex bitr3i 3bitri ancom
      ne0i pm4.71i anbi2i bitr4i snex visset xpex eleq2 ceqsexv bitri opelxp
      elsn eqcom anbi1i an12 eleq1 anbi12d ) AHZEHZIZDJZKVLBHZLMZVNCHZNZVPOZPZC
      FHZQZRZBSZJZKZVPVTKZVLVNKZVORZVSRZBTZRZCTZVJVTKZVKVJKZRZVMWDVLDWCGUAUBWEW
      GWBRZBTWICVTQZBTZWLWBBVLUCWPWQBWQWHWARWPWHVSCVTUDWGVOWAUEUFUGWRWJCVTQWLWI
      CBVTUHWJCVTUIUJUKWLVPVJPZWFVKVPKZRZRZCTWOWKXBCWKWFVLVRKZRWFWSWTRZRXBWJXCW
      FWJVSWGRZBTXCWIXEBWIVSWHRXEWHVSULWGWHVSWGVOVNVLUMUNUOUPUGWGXCBVRVQVPVPUQC
      URUSVNVRVLUTVAVBUOXCXDWFXCVJVQKZWTRXDVJVKVQVPEURVCXFWSWTXFVJVPPWSAVPVDVJV
      PVEVBVFVBUOWFWSWTVGUKUGXAWOCVJAURWSWFWMWTWNVPVJVTVHVPVJVKUTVIVAVBUK $.
      $( [3-Apr-2007] $) $( [12-Apr-2004] $)

    $( Lemma for ~ aceq5 . $)
    aceq5lem3 $p |- ( ( { w } X. w ) e. A <-> ( w =/= (/) /\ w e. h ) ) $=
      ( cv csn cxp c0 wne wceq wrex wa cab wcel snex visset xpex neeq1 eqeq1
      rexbidv anbi12d elab eleq2i xpeq2 xp0 syl6eq crn rneq snnz rnxp ax-mp
      rn0 3eqtr3g impbii necon3bii wex df-rex sneq xpeq1 syl eqtrd eqcom bitri
      anbi2i ancom exbii eleq1 ceqsexv 3bitrri anbi12i 3bitr4i ) AGZHZVNIZBGZJK
      ZVQCGZHZVSIZLZCEGZMZNZBOZPVPJKZVPWALZCWCMZNZVPDPVNJKZVNWCPZNWEWJBVPVOVNVN
      QARZSVQVPLZVRWGWDWIVQVPJTWNWBWHCWCVQVPWAUAUBUCUDDWFVPFUEWKWGWLWIVNJVPJVNJ
      LZVPJLZWOVPVOJIJVNJVOUFVOUGUHWPVPUIZJUIVNJVPJUJVOJKWQVNLVNWMUKVOVNULUMZUN
      UOUPUQWIVSWCPZWHNZCURVSVNLZWSNZCURWLWHCWCUSWTXBCWTWSXANXBWHXAWSWHVNVSLZXA
      WHXCWHWQWAUIZVNVSVPWAUJWRVTJKXDVSLVSCRUKVTVSULUMUOXCVPVTVNIZWAXCVOVTLVPXE
      LVNVSUTVOVTVNVAVBVNVSVTUFVCUPVNVSVDVEVFWSXAVGVEVHWSWLCVNWMVSVNWCVIVJVKVLV
      M $.
      $( [3-Apr-2007] $) $( [12-Apr-2004] $)

    ${
      aceq5lem.2 $e |- B = ( U. A i^i y ) $.
      aceq5lem.3 $e |- ( ph <-> A. x ( ( A. z e. x z =/= (/) /\
             A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
            E. y A. z e. x E! v v e. ( z i^i y ) ) ) $.
      $( Lemma for ~ aceq5 . $)
      aceq5lem4 $p |- ( ph -> E. y A. z e. A E! v v e. ( z i^i y ) ) $=
        ( vg cv c0 wne wral cin wceq wi wcel weu wex csn cxp wrex wa cab
        eleq2i visset neeq1 eqeq1 rexbidv anbi12d elab simplbi sylbi rgen wn
        wal cop eleq2 elxp excom bitri syl6bb bi2anan9 eeanv syl6bbr opeq1
        eqeq2d biimpac elsn sylan2b adantrr 19.23aiv anim12i eqtr2 opth 3syl
        19.23aivv syl6bi sneq xpeq1 syl xpeq2 eqtrd syl6 eqeq12 sylibrd ex a1i
        r19.23aiv a1d r19.23adv imp elab2 simprbi cbvrexv sylib syl2an df-an
        syl5ibr necon1ad 19.21adv disj1 syl6ibr rgen2a cvv abrexex simpr
        ss2abi ssexi eqeltri raleq raleqbi1dv exbidv imbi12d cla4v mp2ani ) ADP
        ZQRZDISZYCEPZRZYCYFTQUAZUBZEISZDISZFPZYCCPZTUCFUDZDISZCUEZYDDIYCIUCZYCG
        PZQRZYRHPZUFZYTUGZUAZHKPZUHZUIZGUJZUCZYDIUUGYCLUKUUHYDYCUUBUAZHUUDUHZUU
        FYDUUJUIZGYCDULZYRYCUAZYSYDUUEUUJYRYCQUMUUMUUCUUIHUUDYRYCUUBUNUOUPZUQUR
        USUTYIDEIYQYFIUCZUIZYGBPZYCUCZUUQYFUCZVAUBZBVBYHUUPYGUUTBUUPUUTYCYFUUPU
        URUUSUIZYCYFUAZUUTVAUUJYFOPZUFZUVCUGZUAZOUUDUHZUVAUVBUBZYQUUOUUJUVGUVHU
        UJUVFUVHOUUDUUJUVFUVHUBZUVCUUDUCUUIUVIHUUDUUIUVIUBYTUUDUCUUIUVFUVHUUIUV
        FUIZUVAUUBUVEUAZUVBUVJUVAYTUVCUAZUVKUVJUVAUUQYRYLVCZUAZYRUUAUCZYLYTUCZU
        IUIZGUEZUUQYRYMVCZUAZYRUVDUCZYMUVCUCZUIUIZGUEZUIZCUEFUEZUVLUVJUVAUVRFUE
        ZUWDCUEZUIUWFUUIUURUWGUVFUUSUWHUUIUURUUQUUBUCZUWGYCUUBUUQVDUWIUVQFUEGUE
        UWGGFUUQUUAYTVEUVQGFVFVGVHUVFUUSUUQUVEUCZUWHYFUVEUUQVDUWJUWCCUEGUEUWHGC
        UUQUVDUVCVEUWCGCVFVGVHVIUVRUWDFCVJVKUWEUVLFCUWEUUQYTYLVCZUAZUUQUVCYMVCZ
        UAZUIUWKUWMUAZUVLUVRUWLUWDUWNUVQUWLGUVNUVOUWLUVPUVNYRYTUAZUWLUVOUWPUVNU
        WLUWPUVMUWKUUQYRYTYLVLVMVNGYTVOVPVQVRUWCUWNGUVTUWAUWNUWBUVTYRUVCUAZUWNU
        WAUWQUVTUWNUWQUVSUWMUUQYRUVCYMVLVMVNGUVCVOVPVQVRVSUUQUWKUWMVTUWOUVLYLYM
        UAYTYLUVCYMHULFULCULWAURWBWCWDUVLUUBUVDYTUGZUVEUVLUUAUVDUAUUBUWRUAYTUVC
        WEUUAUVDYTWFWGYTUVCUVDWHWIZWJYCUUBYFUVEWKWLWMWNWOWPWQWRYQYDUUJUUFUUKGYC
        IUULUUNLWSWTUUOYFUUBUAZHUUDUHZUVGUUOYFQRZUXAUUFUXBUXAUIGYFIEULYRYFUAZYS
        UXBUUEUXAYRYFQUMUXCUUCUWTHUUDYRYFUUBUNUOUPLWSWTUWTUVFHOUUDUVLUUBUVEYFUW
        SVMXAXBXCUURUUSXDXEXFXGBYCYFXHXIXJAYDDUUQSZYIEUUQSZDUUQSZUIZYNDUUQSZCUE
        ZUBZBVBYEYKUIZYPUBZNUXJUXLBIIUUGXKLUUGUUEGUJHGUUDUUBKULXLUUFUUEGYSUUEXM
        XNXOXPUUQIUAZUXGUXKUXIYPUXMUXDYEUXFYKYDDUUQIXQUXEYJDUUQIYIEUUQIXQXRUPUX
        MUXHYOCYNDUUQIXQXSXTYAUSYB $.
        $( [3-Apr-2007] $) $( [11-Apr-2004] $)

      $( Lemma for ~ aceq5 . $)
      aceq5lem5 $p |- ( ph ->
                           E. f A. w e. h ( w =/= (/) -> ( f ` w ) e. w ) ) $=
        ( vg cv cin wcel weu wral wex c0 wne cfv wi aceq5lem4 wa cop simpr a1i
        csn cxp wceq ineq1 eleq2d eubidv rcla4cv aceq5lem3 aceq5lem1 3imtr3g
        jcad cuni eleq2i elin aceq5lem2 anbi1i anass bitri 3bitri eubii euanv
        bitr2i syl6ib euex hbeu1 ax-17 hbim simprbi simpld visset tz6.12
        eleq1d biimparc exp32 mpcom 19.23ai syl6 exp3a com23 r19.21aiv cvv
        inex2 eqeltri fveq1 imbi2d ralbidv cla4ev syl 19.23aiv ) AFQZDQZCQZRZSZ
        FTZDIUAZCUBEQZUCUDZXHKQZUEZXHSZUFZELQZUAZKUBZABCDEFGHIJLMNOUGXGXPCXGXIX
        HJUEZXHSZUFZEXNUAZXPXGXSEXNXGXIXHXNSZXRXGXIYAXRXGXIYAUHZXHPQZUIZJSZPTZX
        RXGYBYAYCXHSZYDXCSZUHZPTZUHZYFXGYBYAYJYBYAUFXGXIYAUJUKXGXHULXHUMZISXAYL
        XCRZSZFTZYBYJXFYODYLIXBYLUNZXEYNFYPXDYMXAXBYLXCUOUPUQUREGHILMUSCEFPUTVA
        VBYFYAYIUHZPTYKYEYQPYEYDIVCZXCRZSYDYRSZYHUHZYQJYSYDNVDYDYRXCVEUUAYAYGUH
        ZYHUHYQYTUUBYHEGHIPLMVFVGYAYGYHVHVIVJZVKYAYIPVLVMVNYEPUBYFXRYEPVOYEYFXR
        UFZPYFXRPYEPVPXRPVQVRYGYEUUDYEYGYHYEYAYIUUCVSVTYGYEYFXRYEYFUHZXRYGUUEXQ
        YCXHPXHJEWAWBWCWDWEWFWGWFWHWIWJWKXOXTKJJYSWLNXCYRCWAWMWNXJJUNZXMXSEXNUU
        FXLXRXIUUFXKXQXHXHXJJWOWCWPWQWRWSWTWS $.
        $( [3-Apr-2007] $) $( [12-Apr-2004] $)
    $}
  $}

  ${
    $d f x z y w v h u t $.
    $( Equivalence of two versions of the Axiom of Choice.  The left-hand side
       is similar to the Axiom of Choice (first form) of [Enderton] p. 49.  The
       right-hand side is Theorem 6M(4) of [Enderton] p. 151 and asserts that
       given a family of mutually disjoint nonempty sets, a set exists
       containing exactly one member from each set in the family.  The proof
       does not depend on AC. $)
    aceq5 $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
           A. x ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
          E. y A. z e. x E! v v e. ( z i^i y ) ) ) $=
      ( vt vh vu cv wss cdm wfn wa wex wal c0 wne wral cin wceq wi wcel weu
      cfv aceq4 crn wb wrex fveq2 id eleq12d neeq2 ineq2 eqeq1d imbi12d
      anbi12d rcla4v wn minel ex imim2d imp necon4ad eleq1 biimpar sylan2
      eqeq2 eqcom syl6bb syl5bi ad2antrl mpd exp32 syl6com com14 r19.23adv
      fvelrnb biimpac syl5 exp3a com4t imp4b elin syl5ib r19.26 sylan2br
      anassrs adantlr fnfvelrn expcom anim12d syl6ibr com13 imp31 syl5cbir
      adantr impbid 19.21adv fvex bibi2d albidv cla4ev df-eu sylibr syl6
      ralimdva neeq1 cbvralv anbi2i bitr4i pm3.35 ancoms ralimi sylbi visset
      rnex eleq2d eubidv ralbidv 19.23aiv alimi csn cxp cab cuni eqid biid
      aceq5lem5 19.21aiv aceq3 raleq exbidv cbvalv bitr2i sylib impbii ) FJZAJZ
      KYRYSLMNFOAPZCJZQRZCYSSZUUADJZRZUUAUUDTZQUAZUBZDYSSZCYSSZNZEJZUUABJZTZUCZ
      EUDZCYSSZBOZUBZAPZYTYRYSMZUUDQRZUUDYRUEZUUDUCZUBZDYSSZNZFOZAPUUTADFUFUVHU
      USAUVGUUSFUVGUUKUULUUAYRUGZTZUCZEUDZCYSSZUURUVAUVFUUCUUJUVMUVAUVFUUCUUJUV
      MUBZUVAUVDDYSSZUVNUVFUUCNZUVAUVOUVNUVAUVONZUUIUVLCYSUVQUUAYSUCZNZUUIUVKUU
      LUUAYRUEZUAZUHZEPZUVLUVSUUIUWBEUVSUUIUWBUVSUUINUVKUWAUVQUUIUVKUWAUBZUVRUV
      AUVOUUIUWDUVAUVDUUHNZDYSSZUWDUVOUUINUVAUWFNUULUUAUCZUULUVIUCZNUWAUVKUVAUW
      FUWGUWHUWAUWGUWHUVAUWFUWAUWGUWHUVAUWFUWAUBZUWGGJZYRUEZUULUAZGYSUIZUWIUWHU
      VANUWGUWLUWIGYSUWFUWJYSUCZUWLUWGUWAUWNUWFUWKUWJUCZUUAUWJRZUUAUWJTZQUAZUBZ
      NZUWLUWGUWAUBUBUWEUWTDUWJYSUUDUWJUAZUVDUWOUUHUWSUXAUVCUWKUUDUWJUUDUWJYRUJ
      UXAUKULUXAUUEUWPUUGUWRUUDUWJUUAUMUXAUUFUWQQUUDUWJUUAUNUOUPUQURUWTUWLUWGUW
      AUWTUWLUWGNZNUUAUWJUAZUWAUWTUWKUUAUCZUXCUXBUWTUXDUXCUWTUXDUUAUWJUWOUWSUWP
      UXDUSZUBUWOUWRUXEUWPUWOUWRUXEUWKUWJUUAUTVAVBVCVDVCUWLUXDUWGUWKUULUUAVEVFV
      GUWLUXCUWAUBUWTUWGUWLUVTUWKUAZUWAUXCUWLUXFUVTUULUAUWAUWKUULUVTVHUVTUULVIV
      JUUAUWJYRUJVKVLVMVNVOVPVQUVAUWHUWMGYSUULYRVRVSVTWAWBWCUULUUAUVIWDWEUVDUUH
      DYSWFWGWHWIUVSUWAUVKUBUUIUWAUVKUVTUVJUCZUVSUULUVTUVJVEUVAUVOUVRUXGUVRUVOU
      VAUXGUVRUVOUVAUXGUVRUVOUVANUVTUUAUCZUVTUVIUCZNUXGUVRUVOUXHUVAUXIUVDUXHDUU
      AYSUUDUUAUAZUVCUVTUUDUUAUUDUUAYRUJUXJUKULURUVAUVRUXIYSUUAYRWJWKWLUVTUUAUV
      IWDWMWAWNWOWPWQWRVAWSUWCUVKUULHJZUAZUHZEPZHOUVLUXNUWCHUVTUUAYRWTUXKUVTUAZ
      UXMUWBEUXOUXLUWAUVKUXKUVTUULVHXAXBXCUVKEHXDXEXFXGVAUVPUVEUVBNZDYSSZUVOUVP
      UVFUVBDYSSZNUXQUUCUXRUVFUUBUVBCDYSUUAUUDQXHXIXJUVEUVBDYSWFXKUXPUVDDYSUVBU
      VEUVDUVBUVDXLXMXNXOVTWAWCUUQUVMBUVIYRFXPXQUUMUVIUAZUUPUVLCYSUXSUUOUVKEUXS
      UUNUVJUULUUMUVIUUAUNXRXSXTXCXFYAYBXOUUTUVEDUXKSZFOZHPZYTUUTUYAHUUTABCDEIG
      IJZQRUYCUWJYCUWJYDUAGUXKUINIYEZUYDYFUUMTZFHUYDYGUYEYGUUTYHYIYJYTUVFFOZAPU
      YBADFYKUYFUYAAHYSUXKUAUVFUXTFUVEDYSUXKYLYMYNYOYPYQ $.
      $( [21-Apr-2007] $) $( [11-Apr-2004] $)
  $}

  ${
    $d x z f y w v u g $.
    $( Our Axiom of Choice (in the form of ~ ac3 ) implies the Axiom of Choice
       (first form) of [Enderton] p. 49.  The proof uses neither AC nor the
       Axiom of Regularity.  See ~ aceq6b for the converse (which does use the
       Axiom of Regularity). $)
    aceq6a $p |- ( A. x E. y A. z e. x ( z =/= (/) -> E! w e. z E. v e. y
                      ( z e. v /\ w e. v ) ) ->
                  A. x E. f ( f C_ x /\ f Fn dom x ) ) $=
      ( vu vg cv c0 wne wcel wa wrex wreu wi wral wex wal cfv wss cdm wfn crab
      cuni wceq copab cab eleq2 eleq1 anbi1d rexbidv anbi12d abbidv df-rab
      3eqtr4g unieqd eqid visset rabex uniex fvopab4 eleq1d reucl syl5bir
      imim2d ralimia opabex2 fveq1 imbi2d ralbidv cla4ev syl 19.23aiv alimi
      aceq3 sylibr ) CIZJKZVREIZLZDIZVTLZMZEBIZNZDVROZPZCAIZQZBRZASVSVRFIZTZVRL
      ZPZCWIQZFRZASWLWIUAWLWIUBUCMFRASWKWQAWJWQBWJVSVRGIZWILHIWRVTLZWCMZEWENZDW
      RUDZUEZUFMGHUGZTZVRLZPZCWIQZWQWHXGCWIVRWILZWGXFVSXIXFWFDVRUDZUEZVRLWGXIXE
      XKVRGHVRXCXKWIXDWRVRUFZXBXJXLWBWRLZXAMZDUHWBVRLZWFMZDUHXBXJXLXNXPDXLXMXOX
      AWFWRVRWBUIXLWTWDEWEXLWSWAWCWRVRVTUJUKULUMUNXADWRUOWFDVRUOUPUQXDURXJWFDVR
      CUSUTVAVBVCWFDVRVDVEVFVGWPXHFXDGHWIXCAUSVHWLXDUFZWOXGCWIXQWNXFVSXQWMXEVRV
      RWLXDVIVCVJVKVLVMVNVOACFVPVQ $.
      $( [30-Apr-2007] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u f g h $.
    $( Axiom of Choice (first form) of [Enderton] p. 49 implies of our Axiom of
       Choice (in the form of ~ ac3 ).  The proof does not make use of AC. Note
       that the Axiom of Regularity is used by the proof.  Specifically,
       ~ elirrv and ~ preleq that are referenced in the proof each make use of
       Regularity for their derivations.  (The reverse implication can be
       derived without using Regularity; see ~ aceq6a .) $)
    aceq6b $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) ->
                  A. x E. y A. z e. x ( z =/= (/) -> E! w e. z E. v e. y
                      ( z e. v /\ w e. v ) ) ) $=
      ( vu vg cv wss cdm wfn wa wex wal c0 wne cfv wcel wi wral wrex wreu
      aceq3 cpr wceq cab hbra1 wmo ra4 fvex eleq1 anbi2d rexbidv anbi12d
      cla4ev eleq2 rcla4ev eqid neeq1 eqeq1 mpanr2 fveq2 preq1d preq2 eqtr2d
      anim2i reximi syl prex elab sylibr visset prid2 prid1 pm3.2i sylancl
      sylan2 ex syl8 imp3a pm2.43d eleq1d bitrd imbi12d rcla4cv wb wn prel12
      ancom syl5rbbr sylan9bbr elirrv mtbii con2i adantrr pm5.32da preleq
      syl6bir eqeq2d biimparc syl6 exp4c com13 com4r imp imp4a com3l r19.23aiv
      sylbi exp3a imp4b 19.23adv df-rex syl5ib expimpd 19.21aiv mo2icl jctird
      weu df-reu eu5 bitri syl6ibr r19.21ai abrexex simpr ss2abi ssexi rexeq
      reubidv imbi2d ralbidv 19.23aiv alimi ) FIZAIZJYPYQKLMFNAOCIZPQZYRYPRZYRS
      ZTZCYQUAZFNZAOYSYREIZSZDIZUUESZMZEBIZUBZDYRUCZTZCYQUAZBNZAOACFUDUUDUUOAUU
      CUUOFUUCYSUUIEGIZPQZHIZUUPYPRZUUPUEZUFZMZGYQUBZHUGZUBZDYRUCZTZCYQUAZUUOUU
      CUVGCYQUUBCYQUHUUCYRYQSZYSUVFUUCUVIYSMZUUGYRSZUVEMZDNZUVLDUIZMZUVFUUCUVJU
      VMUVNUUCUVJUVMUUCUVIYSUVJUVMTZUUCUVIYSUUAUVPUUBCYQUJUUAUVJUVMUUAUUFYTUUES
      ZMZEUVDUBZUVMUVJUVLUUAUVSMDYTYRYPUKZUUGYTUFZUVKUUAUVEUVSUUGYTYRULUWAUUIUV
      REUVDUWAUUHUVQUUFUUGYTUUEULUMUNUOUPYTYRUEZUVDSZYRUWBSZYTUWBSZMZUVSUVJUVRU
      WFEUWBUVDUUEUWBUFUUFUWDUVQUWEUUEUWBYRUQUUEUWBYTUQUOURUVJUUQUWBUUTUFZMZGYQ
      UBZUWCUVJUUQUUPYRUFZMZGYQUBZUWIUVIYSYRYRUFZUWLYRUSUWKYSUWMMGYRYQUWJUUQYSU
      WJUWMUUPYRPUTUUPYRYRVAUOURVBUWKUWHGYQUWJUWGUUQUWJUUTYTUUPUEUWBUWJUUSYTUUP
      UUPYRYPVCVDUUPYRYTVEVFVGVHVIUVCUWIHUWBYTYRVJUURUWBUFZUVBUWHGYQUWNUVAUWGUU
      QUURUWBUUTVAUMUNVKVLUWDUWEYTYRCVMZVNYTYRUVTVOVPVQVRVSVTWAWBUUCUVLUWATZDOU
      VNUUCUWPDUUCUVKUVEUWAUUCUVKMZUUEUVDSZUUIMZENUWAUVEUWQUWSUWAEUUCUVKUWRUUIU
      WAUWRUVKUUCUUIUWATZUWRUVKUUCUWTUWRUUQUUEUUTUFZMZGYQUBZUVKUUCMZUWTTZUVCUXC
      HUUEEVMUURUUEUFZUVBUXBGYQUXFUVAUXAUUQUURUUEUUTVAUMUNVKUXBUXEGYQUXDUUPYQSZ
      UXBUWTUXDUXGUUQUXAUWTUVKUUCUXGUUQUXAUWTTZTTUUCUXGUUQUVKUXHUUCUXGUUQUUSUUP
      SZUVKUXHTUUBUUQUXITCUUPYQYRUUPUFZYSUUQUUAUXIYRUUPPUTUXJUUAUUSYRSUXIUXJYTU
      USYRYRUUPYPVCZWCYRUUPUUSUQWDWEWFUXAUVKUXIUWTUXAUVKUXIUUIUWAUXAUVKUXIMZUUI
      MZUUGUUSUFZUXJMZUWAUXAUXMUXLUUGYRUEUUTUFZMUXOUXAUXLUXPUUIUXAUVKUXPUUIWGZU
      XIUXAUUGYRUFZWHZUXQUVKUXSUXPUUGUUTSZYRUUTSZMZUXAUUIUUGYRUUSUUPDVMZUWOUUPY
      PUKZGVMZWIUXAUUHUUFMUYBUUIUXAUUHUXTUUFUYAUUEUUTUUGUQUUEUUTYRUQUOUUHUUFWJW
      KWLUXRUVKUXRUUGUUGSUVKDWMUUGYRUUGUQWNWOVRWPWQUUGYRUUSUUPUYCUWOUYDUYEWRWSU
      XJUWAUXNUXJYTUUSUUGUXKWTXAXBXCXDVTXEXFXGXHXIXJXKXDXLXMUUIEUVDXNXOXPXQUVLD
      YTXRVIXSUVFUVLDXTUVOUVEDYRYAUVLDYBYCYDXKYEUUNUVHBUVDUVDUVAGYQUBZHUGGHYQUU
      TAVMYFUVCUYFHUVBUVAGYQUUQUVAYGVHYHYIUUJUVDUFZUUMUVGCYQUYGUULUVFYSUYGUUKUV
      EDYRUUIEUUJUVDYJYKYLYMUPVIYNYOXJ $.
      $( [30-Apr-2007] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x z f y w v u $.
    $( Equivalence of the Axiom of Choice (first form) of [Enderton] p. 49 and
       our Axiom of Choice (in the form of ~ ac2 ).  The proof does not depend
       AC on but does depend on the Axiom of Regularity. $)
    aceq7 $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
                  A. x E. y A. z e. x A. w e. z E! v e. z E. u e. y
                      ( z e. u /\ v e. u ) ) $=
      ( cv wss cdm wfn wa wex wal c0 wne wcel wrex wreu wi wral aceq6b aceq6a
      impbii aceq2 albii bitr4i ) GHZAHZIUHUIJKLGMANZCHZOPUKEHZQDHULQLEBHZRDUKS
      TCUIUABMZANZUKFHZQULUPQLFUMREUKSDUKUACUIUABMZANUJUOABCDEGUBABCDEGUCUDUQUN
      AABCDEFUEUFUG $.
      $( [26-Jun-2007] $) $( [5-Apr-2004] $)
  $}

