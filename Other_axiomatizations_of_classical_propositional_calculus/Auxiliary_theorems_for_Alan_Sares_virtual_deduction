$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Auxiliary theorems for Alan Sare's virtual deduction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ee11.1 $e |- ( ph -> ps ) $.
    ee11.2 $e |- ( ph -> ch ) $.
    ee11.3 $e |- ( ps -> ( ch -> th ) ) $.
    $( ~ e11 without virtual deductions. ~ sylc is also ~ e11 without virtual
       deductions, except with a different order of hypotheses.  (Contributed
       by Alan Sare, 8-Jul-2011.) $)
    ee11 $p |- ( ph -> th ) $=
      ( sylc ) BCDAGEFH $.
      $( [8-Jul-2011] $)
  $}

  ${
    iin3.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    $( ~ in3 without virtual deduction connectives.  Special theorem needed for
       Alan Sare's virtual deduction translation tool.  (Contributed by Alan
       Sare, 23-Jul-2011.) $)
    iin3 $p |- ( ph -> ( ps -> ( ch -> th ) ) ) $=
      (  ) E $.
      $( [28-Oct-2011] $) $( [23-Jul-2011] $)
  $}

  $( ~ idn3 without virtual deduction connectives.  Special theorem needed for
     Alan Sare's virtual deduction translation tool.  (Contributed by Alan
     Sare, 23-Jul-2011.) $)
  iidn3 $p |- ( ph -> ( ps -> ( ch -> ch ) ) ) $=
    ( wi id a1i12 ) ABCCDCEF $.
    $( [28-Oct-2011] $) $( [23-Jul-2011] $)

  ${
    ee222.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee222.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee222.3 $e |- ( ph -> ( ps -> ta ) ) $.
    ee222.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e222 without virtual deduction connectives.  Special theorem needed
       for Alan Sare's virtual deduction translation tool.  (Contributed by
       Alan Sare, 7-Jul-2011.) $)
    ee222 $p |- ( ph -> ( ps -> et ) ) $=
      ( wa imp wi sylc mpd ex ) ABFABKZEFABEILCDEFMQJABCGLABDHLNOP $.
      $( [28-Oct-2011] $) $( [7-Jul-2011] $)
  $}

  ${
    ee22.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee22.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee22.3 $e |- ( ch -> ( th -> ta ) ) $.
    $( Virtual deduction rule ~ e22 without virtual deduction connectives.
       Special theorem needed for Alan Sare's virtual deduction translation
       tool.  (Contributed by Alan Sare, 2-May-2011.) $)
    ee22 $p |- ( ph -> ( ps -> ta ) ) $=
      ( wi syl6 mpdd ) ABDEGABCDEIFHJK $.
      $( [28-Oct-2011] $) $( [2-May-2011] $)
  $}

  ${
    ee12an.1 $e |- ( ph -> ps ) $.
    ee12an.2 $e |- ( ph -> ( ch -> th ) ) $.
    ee12an.3 $e |- ( ( ps /\ th ) -> ta ) $.
    $( ~ e12an without virtual deduction connectives.  Special theorem needed
       for Alan Sare's virtual deduction translation tool.  (Contributed by
       Alan Sare, 28-Oct-2011.) $)
    ee12an $p |- ( ph -> ( ch -> ta ) ) $=
      ( a1d ex ee22 ) ACBDEABCFIGBDEHJK $.
      $( [28-Oct-2011] $) $( [28-Oct-2011] $)
  $}

  ${
    ee3bir.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee3bir.2 $e |- ( ta <-> th ) $.
    $( Right-biconditional form of ~ e3 without virtual deduction connectives.
       Special theorem needed for Alan Sare's virtual deduction translation
       tool.  (Contributed by Alan Sare, 22-Jul-2011.) $)
    ee3bir $p |- ( ph -> ( ps -> ( ch -> ta ) ) ) $=
      ( bicomi syl8ib ) ABCDEFEDGHI $.
      $( [28-Oct-2011] $) $( [22-Jul-2011] $)
  $}

  ${
    ee13.1 $e |- ( ph -> ps ) $.
    ee13.2 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    ee13.3 $e |- ( ps -> ( ta -> et ) ) $.
    $( ~ e13 without virtual deduction connectives.  Special theorem needed for
       Alan Sare's virtual deduction translation tool.  (Contributed by Alan
       Sare, 28-Oct-2011.) $)
    ee13 $p |- ( ph -> ( ch -> ( th -> et ) ) ) $=
      ( wi com12 syl8 com34 syl7 pm2.43a ) CADFJZACBPAACDBFACDEBFJHBEFIKLMGNO
      $.
      $( [28-Oct-2011] $)
  $}

  ${
    ee23.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee23.2 $e |- ( ph -> ( ps -> ( th -> ta ) ) ) $.
    ee23.3 $e |- ( ch -> ( ta -> et ) ) $.
    $( ~ e23 without virtual deductions.  (Contributed by Alan Sare,
       17-Jul-2011.) $)
    ee23 $p |- ( ph -> ( ps -> ( th -> et ) ) ) $=
      ( wi syl6 com3r syl8 com24 pm2.43i pm2.43d ) ABDFJADBBFADBBFJZJJABDAQABDE
      AQJHABEFABCEFJGIKLMNONP $.
      $( [17-Jul-2011] $)
  $}

  ${
    ee121.1 $e |- ( ph -> ps ) $.
    ee121.2 $e |- ( ph -> ( ch -> th ) ) $.
    ee121.3 $e |- ( ph -> ta ) $.
    ee121.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e121 without virtual deductions.  (Contributed by Alan Sare,
       13-Jul-2011.) $)
    ee121 $p |- ( ph -> ( ch -> et ) ) $=
      ( a1d ee222 ) ACBDEFABCGKHAECIKJL $.
      $( [13-Jul-2011] $)
  $}

  ${
    ee122.1 $e |- ( ph -> ps ) $.
    ee122.2 $e |- ( ph -> ( ch -> th ) ) $.
    ee122.3 $e |- ( ph -> ( ch -> ta ) ) $.
    ee122.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e122 without virtual deductions.  (Contributed by Alan Sare,
       13-Jul-2011.) $)
    ee122 $p |- ( ph -> ( ch -> et ) ) $=
      ( a1d ee222 ) ACBDEFABCGKHIJL $.
      $( [13-Jul-2011] $)
  $}

  $( If the second and third disjuncts of a true triple disjunction are false,
     then the first disjunct is true.  Automatically derived from
     ~ 3ornot23VD .  (Contributed by Alan Sare, 31-Dec-2011.) $)
  3ornot23 $p |- ( ( -. ph /\ -. ps ) -> ( ( ch \/ ph \/ ps ) -> ch ) ) $=
    ( wn wa wo w3o ioran biimpri wi 3orass biimpi a1i orel2 sylsyld ) ADBDEZABF
    ZDZCABGZCQFZCRPABHISTJPSTCABKLMQCNO $.
    $( [31-Dec-2011] $)

  $( ~ orbi1 with order of disjuncts reversed.  Derived from ~ orbi1rVD .
     (Contributed by Alan Sare, 31-Dec-2011.) $)
  orbi1r $p |- ( ( ph <-> ps ) -> ( ( ch \/ ph ) <-> ( ch \/ ps ) ) ) $=
    ( wb wo orbi1 pm1.4 syl5bi syl6 syl5bir impbid ) ABDZCAEZCBEZLMBCEZNLACEZOM
    ABCFZCAGHBCGILNPMLPONQCBGJACGIK $.
    $( [31-Dec-2011] $)

  $( Closed nested implication form of ~ bitr3i .  Derived automatically from
     ~ bitr3VD .  (Contributed by Alan Sare, 31-Dec-2011.) $)
  bitr3 $p |- ( ( ph <-> ps ) -> ( ( ph <-> ch ) -> ( ps <-> ch ) ) ) $=
    ( wb id bicomd wi a1i biantr ee12an ) ABDZBADACDZCADZBCDKABKEFLMGKLACLEFHBA
    CIJ $.
    $( [31-Dec-2011] $)

  $( ~ pm4.39 with a 3-conjunct antecedent.  This proof is ~ 3orbi123VD
     automatically translated and minimized.  (Contributed by Alan Sare,
     31-Dec-2011). $)
  3orbi123 $p |- ( ( ( ph <-> ps ) /\ ( ch <-> th ) /\ ( ta <-> et ) ) ->
                 ( ( ph \/ ch \/ ta ) <-> ( ps \/ th \/ et ) ) ) $=
    ( wb w3a 3simp1 3simp2 3simp3 3orbi123d ) ABGZCDGZEFGZHABCDEFMNOIMNOJMNOKL
    $.
    $( [31-Dec-2011] $)

  $( Exportation implication also converting head from biconditional to
     conditional.  This proof is ~ exbirVD automatically translated and
     minimized.  (Contributed by Alan Sare, 31-Dec-2011.) $)
  exbir $p |- ( ( ( ph /\ ps ) -> ( ch <-> th ) ) ->
              ( ph -> ( ps -> ( th -> ch ) ) ) ) $=
    ( wa wb wi bi2 imim2i exp3a ) ABEZCDFZGABDCGZLMKCDHIJ $.
    $( [31-Dec-2011] $)

  $( ~ impexp with a 3-conjunct antecedent.  This proof is ~ 3impexpVD
     automatically translated and minimized.  (Contributed by Alan Sare,
     31-Dec-2011.) $)
  3impexp $p |- ( ( ( ph /\ ps /\ ch ) -> th ) <->
                ( ph -> ( ps -> ( ch -> th ) ) ) ) $=
    ( w3a wi wa wb df-3an imbi1 biimpcd mpi exp4c pm3.31 imp3a syl5ib impbii )
    ABCEZDFZABCDFZFFZSABCDSRABGZCGZHZUCDFZABCIZUDSUERUCDJKLMUAUCDRUAUBCDABTNOUF
    PQ $.
    $( [31-Dec-2011] $)

  $( ~ 3impexp with biconditional consequent of antecedent that is commuted in
     consequent.  Derived automatically from ~ 3impexpVD .  (Contributed by
     Alan Sare, 31-Dec-2011.) $)
  3impexpbicom $p |- ( ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) <->
                     ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ) $=
    ( w3a wb wi bicom imbi2 biimpcd mpi 3expd 3impexp biimpri syl6ibr impbii )
    ABCFZDEGZHZABCEDGZHHHZTABCUATSUAGZRUAHZDEIZUCTUDSUARJKLMUBRUASUDUBABCUANOUE
    PQ $.
    $( [31-Dec-2011] $)

  ${
    3impexpbicomi.1 $e |- ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) $.
    $( Deduction form of ~ 3impexpbicom .  Derived automatically from
       ~ 3impexpbicomiVD .  (Contributed by Alan Sare, 31-Dec-2011.) $)
    3impexpbicomi $p |- ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) $=
      ( w3a wb wi 3impexpbicom mpbi ) ABCGDEHIABCEDHIIIFABCDEJK $.
      $( [31-Dec-2011] $)
  $}

  $( Closed form of ~ syl5 .  Derived automatically from ~ syl5impVD .
     (Contributed by Alan Sare, 31-Dec-2011.) $)
  syl5imp $p |- ( ( ph -> ( ps -> ch ) ) -> ( ( th -> ps ) ->
                ( ph -> ( th -> ch ) ) ) ) $=
    ( wi pm2.04 imim2d com34 ) ABCEEZDBEDACIBACEDABCFGH $.
    $( [31-Dec-2011] $)

  ${
    idi.1 $e |- ph $.
    $( Deduction form of ~ id .  Derived automatically from ~ idiVD .
       (Contributed by Alan Sare, 31-Dec-2011.) $)
    idi $p |- ph $=
      ( id ax-mp ) AABACD $.
      $( [31-Dec-2011] $)
  $}

  $( Closed form of ~ ancoms .  Derived automatically from ~ ancomsimpVD .
     (Contributed by Alan Sare, 31-Dec-2011.) $)
  ancomsimp $p |- ( ( ( ph /\ ps ) -> ch ) <-> ( ( ps /\ ph ) -> ch ) ) $=
    ( wa ancom imbi1i ) ABDBADCABEF $.
    $( [31-Dec-2011] $)


