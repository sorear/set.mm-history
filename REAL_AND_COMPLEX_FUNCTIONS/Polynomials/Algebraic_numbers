$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Algebraic numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c AA $.

  $( Extend class notation to include the set of algebraic numbers. $)
  caa $a class AA $. $( Blackboard bold capital A $)

  $( Define the set of algebraic numbers.  An algebraic number is a root of a
     nonzero polynomial over the integers.  Here we construct it as the union
     of all kernels (preimages of ` { 0 } ` ) of all polynomials in
     ` ( Poly `` ZZ ) ` , except the zero polynomial ` 0p ` . $)
  df-aa $a |- AA = U_ f e. ( ( Poly ` ZZ ) \ { 0p } ) ( `' f " { 0 } ) $.

  ${
    $d f g k m n x y z A $.  $d f k m n z B $.  $d i j P $.  $d i j k m z ph $.
    $d f i j m z F $.  $d i j k n x y K $.  $d i j k m n x y N $.
    $d f k m z R $.
    $( Elementhood in the set of algebraic numbers.  (Contributed by Mario
       Carneiro, 22-Jul-2014.) $)
    elaa $p |- ( A e. AA <-> ( A e. CC /\
      E. f e. ( ( Poly ` ZZ ) \ { 0p } ) ( f ` A ) = 0 ) ) $=
      ( caa wcel cz cply cfv c0p csn cdif cv ccnv cc0 cima ciun wrex cc wceq wa
      df-aa eleq2i eliun wfn wb wf eldifi plyf ffn 0cn fniniseg sylancl rexbiia
      3syl r19.42v bitri 3bitri ) ACDABEFGZHIZJZBKZLMINZOZDAVADZBUSPZAQDZAUTGMR
      ZBUSPSZCVBABTUABAUSVAUBVDVEVFSZBUSPVGVCVHBUSUTUSDZUTQUCZMQDVCVHUDVIUTUQDQ
      QUTUEVJUTUQURUFEUTUGQQUTUHUMUIQMAUTQUJUKULVEVFBUSUNUOUP $.
      $( [22-Jul-2014] $)

    $( An algebraic number is a complex number.  (Contributed by Mario
       Carneiro, 23-Jul-2014.) $)
    aacn $p |- ( A e. AA -> A e. CC ) $=
      ( vf caa wcel cc cv cfv cc0 wceq cz cply c0p csn cdif wrex elaa simplbi )
      ACDAEDABFGHIBJKGLMNOABPQ $.
      $( [23-Jul-2014] $)

    $( The algebraic numbers are a subset of the complex numbers.  (Contributed
       by Mario Carneiro, 23-Jul-2014.) $)
    aasscn $p |- AA C_ CC $=
      ( vx caa cc cv aacn ssriv ) ABCADEF $.
      $( [23-Jul-2014] $)

    ${
      elqaa.1 $e |- ( ph -> A e. CC ) $.
      elqaa.2 $e |- ( ph -> F e. ( ( Poly ` QQ ) \ { 0p } ) ) $.
      elqaa.3 $e |- ( ph -> ( F ` A ) = 0 ) $.
      elqaa.4 $e |- B = ( coeff ` F ) $.
      elqaa.5 $e |- N = ( k e. NN0 |-> sup (
        { n e. NN | ( ( B ` k ) x. n ) e. ZZ } , RR , `' < ) ) $.
      elqaa.6 $e |- R = ( seq 0 ( x. , N ) ` ( deg ` F ) ) $.
      $( Lemma for ~ elqaa .  The function ` N ` represents the denominators of
         the rational coefficients ` B ` .  By multiplying them all together to
         make ` R ` , we get a number big enough to clear all the denominators
         and make ` R x. F ` an integer polynomial. $)
      elqaalem1 $p |- ( ( ph /\ K e. NN0 ) ->
        ( ( N ` K ) e. NN /\ ( ( B ` K ) x. ( N ` K ) ) e. ZZ ) ) $=
        ( wcel cz cn cr cq cn0 wa cfv cv cmul co crab clt ccnv csup wceq oveq1d
        fveq2 eleq1d rabbidv supeq1d wor ltso cnvso mpbi supex fvmpt adantl cuz
        c1 wss c0 wne ssrab2 nnuz sseqtri wrex cply cc0 c0p csn cdif eldifi syl
        wf 0z ax-mp coef2 sylancl ffvelrn sylan qmulz sylibr infmssuzcl sylancr
        zq rabn0 eqeltrd oveq2 elrab sylib ) AHUAPZUBZHIUCZHCUCZFUDZUEUFZQPZFRU
        GZPWSRPWTWSUEUFZQPZUBWRWSXDSUHUIZUJZXDWQWSXHUKAEHEUDZCUCZXAUEUFZQPZFRUG
        ZSXGUJXHUAIXIHUKZSXMXDXGXNXLXCFRXNXKXBQXNXJWTXAUEXIHCUMULUNUOUPNSXDXGSU
        HUQSXGUQURSUHUSUTVAVBVCWRXDVEVDUCZVFXDVGVHZXHXDPXDRXOXCFRVIVJVKWRXCFRVL
        ZXPWRWTTPZXQAUATCVTZWQXRAGTVMUCZPZVNTPZXSAGXTVOVPZVQPYAKGXTYCVRVSVNQPYB
        WAVNWKWBCTGMWCWDUATHCWEWFFWTWGVSXCFRWLWHXDVEWIWJWMXCXFFWSRXAWSUKXBXEQXA
        WSWTUEWNUNWOWP $.
        $( [23-Jul-2014] $)

      ${
        elqaa.7 $e |- P = ( x e. _V , y e. _V |->
          ( ( x x. y ) mod ( N ` K ) ) ) $.
        $( Lemma for ~ elqaa . $)
        elqaalem2 $p |- ( ( ph /\ K e. ( 0 ... ( deg ` F ) ) ) ->
          ( R mod ( N ` K ) ) = 0 ) $=
          ( wcel vi vj vm cc0 cdgr cfv cfz co wa cmo cmpt cn0 cseq wceq elfznn0
          cn cv fveq2i nnmulcl adantl cz elqaalem1 simpld adantlr sylan2 cuz cq
          cmul cply c0p csn cdif eldifi syl dgrcl nn0uz syl6eleq adantr crp nnz
          cr ad2antll zmodcl syl2anc nn0re nnre ad2antrl nnrp modabs2 syl221anc
          modmul1 cc recnd mulcom oveq1d 3eqtr4rd nn0z eqtr4d eqid ovex oveq12d
          oveq1 fvmpt cvv oveq12 ovmpt2a mp2an syl6eq 3eqtr4d seqhomo syl5eq wf
          fveq2 0z a1i seqf ffvelrn syl5eqel vex nn0mulcl syl2anr crab clt ccnv
          csup eleq1d rabbidv supeq1d eqtri fmptd syl2an elexi nn0cn mul02 0mod
          cbvmptv 3syl sylan9eqr mul01 c1 simpr cdiv wne nncn nnne0 1z syl6eqel
          divid wb mod0 mpbird eqtrd seqz 3eqtr3d ) AKUDJUEUFZUGUHZTZUIZGHUPHUQ
          ZKLUFZUJUHZUKZUFZUUOFHULUUSLUFZUUTUJUHZUKZUDUMUFZGUUTUJUHZUDUUQAKULTZ
          UVCUVGUNKUUOUOZAUVIUIZUVCUUOVHLUDUMZUFZUVBUFUVGGUVMUVBRURUVKUAUBVHFUP
          LUVFUVBUDUUOUAUQZUPTZUBUQZUPTZUIZUVNUVPVHUHZUPTZUVKUVNUVPUSZUTZUVNUUP
          TZUVKUVNULTZUVNLUFZUPTZUVNUUOUOZAUWDUWFUVIAUWDUIUWFUVNEUFUWEVHUHVATAD
          EGHIJUVNLMNOPQRVBVCZVDZVEAUUOUDVFUFZTUVIAUUOULUWJAJVGVIUFZTZUUOULTZAJ
          UWKVJVKZVLTUWLNJUWKUWNVMVNVGJVOVNZVPVQVRUVKUVRUIZUVSUUTUJUHZUVNUUTUJU
          HZUVPUUTUJUHZVHUHZUUTUJUHZUVSUVBUFZUVNUVBUFZUVPUVBUFZFUHZUWPUWQUVNUWS
          VHUHZUUTUJUHZUXAUWPUWSUVNVHUHZUUTUJUHZUVPUVNVHUHZUUTUJUHZUXGUWQUWPUWS
          WATZUVPWATZUVNVATZUUTVSTZUWSUUTUJUHUWSUNZUXIUXKUNUWPUWSULTZUXLUWPUVPV
          ATZUUTUPTZUXQUVQUXRUVKUVOUVPVTWBUVKUXSUVRUVKUXSKEUFUUTVHUHVATADEGHIJK
          LMNOPQRVBVCZVRZUVPUUTWCWDZUWSWEVNZUVQUXMUVKUVOUVPWFWBZUVOUXNUVKUVQUVN
          VTWGZUWPUXSUXOUYAUUTWHZVNZUWPUXMUXOUXPUYDUYGUVPUUTWIWDUWSUVPUVNUUTWKW
          JUWPUXFUXHUUTUJUWPUVNWLTZUWSWLTUXFUXHUNUWPUVNUVOUVNWATZUVKUVQUVNWFWGZ
          WMZUWPUWSUYCWMUVNUWSWNWDWOUWPUVSUXJUUTUJUWPUYHUVPWLTUVSUXJUNUYKUWPUVP
          UYDWMUVNUVPWNWDWOWPUWPUWRWATZUYIUWSVATZUXOUWRUUTUJUHUWRUNZUXAUXGUNUWP
          UWRULTZUYLUWPUXNUXSUYOUYEUYAUVNUUTWCWDUWRWEVNUYJUWPUXQUYMUYBUWSWQVNUY
          GUWPUYIUXOUYNUYJUYGUVNUUTWIWDUWRUVNUWSUUTWKWJWRUWPUVTUXBUWQUNUWBHUVSU
          VAUWQUPUVBUUSUVSUUTUJXBUVBWSZUVSUUTUJWTZXCVNUWPUXEUWRUWSFUHZUXAUWPUXC
          UWRUXDUWSFUVOUXCUWRUNUVKUVQHUVNUVAUWRUPUVBUUSUVNUUTUJXBUYPUVNUUTUJWTZ
          XCWGUVQUXDUWSUNUVKUVOHUVPUVAUWSUPUVBUUSUVPUUTUJXBUYPUVPUUTUJWTZXCWBXA
          UWRXDTUWSXDTUYRUXAUNUYSUYTBCUWRUWSXDXDBUQZCUQZVHUHZUUTUJUHZUXAFVUAUWR
          UNVUBUWSUNUIVUCUWTUUTUJVUAUWRVUBUWSVHXEWOSUWTUUTUJWTXFXGXHXIUWCUVKUWD
          UWEUVBUFZUVNUVFUFZUNUWGUVKUWDUIZVUEUWEUUTUJUHZVUFVUGUWFVUEVUHUNUWIHUW
          EUVAVUHUPUVBUUSUWEUUTUJXBUYPUWEUUTUJWTZXCVNUWDVUFVUHUNUVKHUVNUVEVUHUL
          UVFUUSUVNUNUVDUWEUUTUJUUSUVNLXMWOUVFWSZVUIXCUTWRVEXJXKVEUUQAUVIUVCUVH
          UNZUVJUVKGUPTZVUKAVULUVIAGUVMUPRAULUPUVLXLUWMUVMUPTAUAUBVHUPLUDULVPUD
          VATAXNXOUWHUVRUVTAUWAUTXPUWOULUPUUOUVLXQWDXRVRHGUVAUVHUPUVBUUSGUUTUJX
          BUYPGUUTUJWTXCVNVEUURUAUBFULUVFKUDUUOULUDUURUWDUVPULTUIZUIUVNUVPFUHZU
          WQULUVNXDTZUVPXDTVUNUWQUNUAXSZUBXSBCUVNUVPXDXDVUDUWQFVUAUVNUNZVUBUVPU
          NUIVUCUVSUUTUJVUAUVNVUBUVPVHXEWOSUYQXFXGVUMUVSVATZUXSUWQULTUURVUMUVSU
          LTVURUVNUVPXTUVSWQVNUUQAUVIUXSUVJUXTVEZUVSUUTWCYAXRUURULULUVFXLZUWDVU
          FULTUWCUUQAUVIVUTUVJUVKHULUVEULUVFUVKUUSULTZUIZUVDVATZUXSUVEULTVVBUVD
          UPTZVVCAVVAVVDUVIAVVAUIVVDUUSEUFZUVDVHUHVATADEGUCIJUUSLMNOPLHULVVEIUQ
          ZVHUHZVATZIUPYBZWAYCYDZYEZUKUCULUCUQZEUFZVVFVHUHZVATZIUPYBZWAVVJYEZUK
          QHUCULVVKVVQUUSVVLUNZWAVVIVVPVVJVVRVVHVVOIUPVVRVVGVVNVAVVRVVEVVMVVFVH
          UUSVVLEXMWOYFYGYHYPYIRVBVCVDUVDVTVNUVKUXSVVAUXTVRUVDUUTWCWDVUJYJVEUWG
          ULULUVNUVFXQYKUURUWDUIZUDUVNFUHZUDUVNVHUHZUUTUJUHZUDUDXDTZVUOVVTVWBUN
          UDVAXNYLZVUPBCUDUVNXDXDVUDVWBFVUAUDUNVUBUVNUNUIVUCVWAUUTUJVUAUDVUBUVN
          VHXEWOSVWAUUTUJWTXFXGUWDUURVWBUDUUTUJUHZUDUWDVWAUDUUTUJUWDUYHVWAUDUNU
          VNYMZUVNYNVNWOUURUXSUXOVWEUDUNVUSUYFUUTYOYQZYRXKVVSUVNUDFUHZUVNUDVHUH
          ZUUTUJUHZUDVUOVWCVWHVWJUNVUPVWDBCUVNUDXDXDVUDVWJFVUQVUBUDUNUIVUCVWIUU
          TUJVUAUVNVUBUDVHXEWOSVWIUUTUJWTXFXGUWDUURVWJVWEUDUWDVWIUDUUTUJUWDUYHV
          WIUDUNVWFUVNYSVNWOVWGYRXKAUUQUUAAUWMUUQUWOVRUURKUVFUFZUUTUUTUJUHZUDUU
          RUVIVWKVWLUNUUQUVIAUVJUTHKUVEVWLULUVFUUSKUNUVDUUTUUTUJUUSKLXMWOVUJUUT
          UUTUJWTXCVNUURVWLUDUNZUUTUUTUUBUHZVATZUURVWNYTVAUURUUTWLTZUUTUDUUCZVW
          NYTUNUURUXSVWPVUSUUTUUDVNUURUXSVWQVUSUUTUUEVNUUTUUHWDUUFUUGUURUUTWATZ
          UXOVWMVWOUUIUURUXSVWRVUSUUTWFVNUURUXSUXOVUSUYFVNUUTUUTUUJWDUUKUULUUMU
          UN $.
          $( [23-Jul-2014] $)
      $}

      $( Lemma for ~ elqaa . $)
      elqaalem3 $p |- ( ph -> A e. AA ) $=
        ( cc wcel cc0 wceq cmul co vf vz vm vx vy cv cfv cply c0p csn cdif wrex
        cz caa cxp cof wne cdgr cfz cexp csu cmpt cvv cnex wa cseq fvex eqeltri
        a1i fconstmpt wfn wf cq eldifsn sylib simpld plyf syl ffn offval2 fzfid
        dffn5v cn cn0 nn0uz 0z crab ssrab2 cr clt ccnv csup fveq2 oveq1d eleq1d
        rabbidv supeq1d wor ltso cnvso supex fvmpt adantl c1 cuz wss c0 sseqtri
        mpbi zq ax-mp coef2 sylancl ffvelrn sylan qmulz rabn0 sylibr infmssuzcl
        nnuz sylancr eqeltrd sseldi nnmulcl syl2anc adantr sylan2 oveq2d mulass
        nncn eqid syl3anc 3eqtr4d mpteq2dva eqtrd cdiv nnne0 eqcomd sylanbrc
        cmo dgrcl syl5eqel elfznn0 coef3 expcl adantll mulcl fsummulc2 sumeq2dv
        seqf coeid2 zsscn mulcom divcan2 divcl 3eqtrd oveq2 elrab simprbi cmpt2
        elqaalem2 wb crp nnre nnrp mod0 syl2an mpbid zmulcl elplyd simprd oveq1
        divcan3 ovex mpteq2dv 0cn df-0p eqtri eqeq12d syl5ib necon3d mpd fconst
        div0 inidm fvconst2 ofval mpdan mul01 fveq1 eqeq1d rcla4ev elaa ) ABOPZ
        BUAUFZUGZQRZUAUMUHUGZUIUJZUKZULZBUNPIAODUJZUOZGSUPTZUWTPZBUXDUGZQRZUXAA
        UXDUWRPUXDUIUQZUXEAUXDUBOQGURUGZUSTZDUCUFZCUGZSTZUBUFZUXKUTTZSTZUCVAZVB
        ZUWRAUXDUBODUXNGUGZSTZVBUXRAUBODUXSSUXCGVCVCVCOVCPAVDVIZDVCPAUXNOPZVEZD
        UXISHQVFZUGZVCNUXIUYDVGVHZVIZUXSVCPUYCUXNGVGVIUXCUBODVBRAUBODVJVIZAGOVK
        ZGUBOUXSVBZRAOOGVLZUYIAGVMUHUGZPZUYKAUYMGUIUQZAGUYLUWSUKPUYMUYNVEJGUYLU
        IVNVOZVPZVMGVQVRZOOGVSVRZUBOGWBVOZVTZAUBOUXTUXQUYCDUXJUXLUXOSTZUCVAZSTU
        XJDVUASTZUCVAUXTUXQUYCUXJVUADUCUYCQUXIWAADOPZUYBADWCPZVUDADUYEWCNAWDWCU
        YDVLUXIWDPZUYEWCPAUCESWCHQWDWEQUMPZAWFVIAUXKWDPZVEZUXLFUFZSTZUMPZFWCWGZ
        WCUXKHUGZVULFWCWHZVUIVUNVUMWIWJWKZWLZVUMVUHVUNVUQRAEUXKEUFZCUGZVUJSTZUM
        PZFWCWGZWIVUPWLVUQWDHVURUXKRZWIVVBVUMVUPVVCVVAVULFWCVVCVUTVUKUMVVCVUSUX
        LVUJSVURUXKCWMWNWOWPWQMWIVUMVUPWIWJWRWIVUPWRWSWIWJWTXIXAXBXCVUIVUMXDXEU
        GZXFVUMXGUQZVUQVUMPVUMWCVVDVUOXTXHVUIVULFWCULZVVEVUIUXLVMPZVVFAWDVMCVLZ
        VUHVVGAUYMQVMPZVVHUYPVUGVVIWFQXJXKCVMGLXLXMWDVMUXKCXNXOFUXLXPVRVULFWCXQ
        XRVUMXDXSYAYBZYCZUXKWCPVURWCPVEUXKVURSTWCPAUXKVURYDXCUUJAUYMVUFUYPVMGUU
        AVRZWDWCUXIUYDXNYEUUBZDYJVRZYFZUXKUXJPZUYCVUHVUAOPZUXKUXIUUCZUYCVUHVEZU
        XLOPZUXOOPZVVQUYCWDOCVLZVUHVVTAVWBUYBAUYMVWBUYPCVMGLUUDVRZYFWDOUXKCXNZX
        OZUYBVUHVWAAUXNUXKUUEUUFZUXLUXOUUGYEYGUUHUYCUXSVUBDSAUYMUYBUXSVUBRUYPCV
        MUCGUXIUXNLUXIYKUUKXOYHUYCUXJUXPVUCUCVVPUYCVUHUXPVUCRZVVRVVSVUDVVTVWAVW
        GUYCVUDVUHVVOYFVWEVWFDUXLUXOYIYLYGUUIYMYNYOAUBUXMUMUCUXIUMOXFAUULVIVVLA
        VVPVEZUXMUXLVUNSTZDVUNYPTZSTZUMVVPAVUHUXMVWKRVVRVUIUXMUXLDSTZUXLVUNVWJS
        TZSTZVWKVUIVUDVVTUXMVWLRAVUDVUHVVNYFZAVWBVUHVVTVWCVWDXOZDUXLUUMYEVUIVWN
        VWLVUIVWMDUXLSVUIVUDVUNOPZVUNQUQZVWMDRVWOVUIVUNWCPZVWQVVKVUNYJVRZVUIVWS
        VWRVVKVUNYQVRZDVUNUUNYLYHYRVUIVWKVWNVUIVVTVWQVWJOPZVWKVWNRVWPVWTVUIVUDV
        WQVWRVXBVWOVWTVXADVUNUUOYLUXLVUNVWJYIYLYRUUPYGVWHVWIUMPZVWJUMPZVWKUMPVV
        PAVUHVXCVVRVUIVUNVUMPZVXCVVJVXEVWSVXCVULVXCFVUNWCVUJVUNRVUKVWIUMVUJVUNU
        XLSUUQWOUURUUSVRYGVWHDVUNYTTQRZVXDAUDUEBCUDUEVCVCUDUFUEUFSTVUNYTTUUTZDE
        FGUXKHIJKLMNVXGYKUVAVWHVUEVWSVXFVXDUVBZAVUEVVPVVMYFVVPAVUHVWSVVRVVKYGVU
        EDWIPVUNUVCPVXHVWSDUVDVUNUVEDVUNUVFUVGYEUVHVWIVWJUVIYEYBUVJYBAUYNUXHAUY
        MUYNUYOUVKAUXDUIGUIUXDUIRUXDUXCYPUPZTZUIUXCVXITZRAGUIRUXDUIUXCVXIUVLAVX
        JGVXKUIAUBOUXTDYPTZVBUYJVXJGAUBOVXLUXSUYCUXSOPZVUDDQUQZVXLUXSRAUYKUYBVX
        MUYQOOUXNGXNXOVVOAVXNUYBAVUEVXNVVMDYQVRZYFUXSDUVMYLYNAUBOUXTDYPUXDUXCVC
        VCVCUYAUXTVCPUYCDUXSSUVNVIUYGUYTUYHVTUYSYMAUBOQDYPTZVBUBOQVBZVXKUIAUBOV
        XPQAVUDVXNVXPQRVVNVXODUWDYEUVOAUBOQDYPUIUXCVCOVCUYAQOPUYCUVPVIUYGUIVXQR
        AUIOQUJUOVXQUVQUBOQVJUVRVIZUYHVTVXRYMUVSUVTUWAUWBUXDUWRUIVNYSAUXFDQSTZQ
        AUWNUXFVXSRIAOODQSOUXCGVCVCBAOUXBUXCVLZUXCOVKVXTAODUYFUWCVIOUXBUXCVSVRU
        YRUYAUYAOUWEUWNBUXCUGDRAODBUYFUWFXCABGUGQRUWNKYFUWGUWHAVUDVXSQRVVNDUWIV
        RYOUWQUXGUAUXDUWTUWOUXDRUWPUXFQBUWOUXDUWJUWKUWLYEBUAUWMYS $.
        $( [23-Jul-2014] $)
    $}

    $( The set of numbers generated by the roots of polynomials in the
       _rational numbers_ is the same as the set of algebraic numbers, which by
       ~ elaa are defined only in terms of polynomials over the integers.
       (Contributed by Mario Carneiro, 23-Jul-2014.) $)
    elqaa $p |- ( A e. AA <-> ( A e. CC /\
      E. f e. ( ( Poly ` QQ ) \ { 0p } ) ( f ` A ) = 0 ) ) $=
      ( vm vj vk vn wcel cc cv cfv wceq cq wa cz wss cmul co cn crab cr caa cc0
      cply c0p csn cdif wrex elaa wi zssq qsscn plyss mp2an ssdif ssrexv anim2i
      mp2b sylbi ccoe cdgr cn0 clt ccnv csup cmpt cseq simpll simplr simpr eqid
      fveq2 oveq1d eleq1d rabbidv cbvrabv syl6eq supeq1d elqaalem3 ex rexlimdva
      oveq2 cbvmptv imp impbii ) AUAGZAHGZABIZJUBKZBLUCJZUDUEZUFZUGZMZWEWFWHBNU
      CJZWJUFZUGZMWMABUHWPWLWFWNWIOZWOWKOWPWLUINLOLHOWQUJUKNLULUMWNWIWJUNWHBWOW
      KUOUQUPURWFWLWEWFWHWEBWKWFWGWKGZMZWHWEWSWHMAWGUSJZWGUTJPCVACIZWTJZDIZPQZN
      GZDRSZTVBVCZVDZVEZUBVFJZEFWGXIWFWRWHVGWFWRWHVHWSWHVIWTVJCEVAXHEIZWTJZFIZP
      QZNGZFRSZTXGVDXAXKKZTXFXPXGXQXFXLXCPQZNGZDRSXPXQXEXSDRXQXDXRNXQXBXLXCPXAX
      KWTVKVLVMVNXSXODFRXCXMKXRXNNXCXMXLPWAVMVOVPVQWBXJVJVRVSVTWCWD $.
      $( [23-Jul-2014] $)

    $( Every rational number is algebraic.  (Contributed by Mario Carneiro,
       23-Jul-2014.) $)
    qaa $p |- ( A e. QQ -> A e. AA ) $=
      ( vf cq wcel cc cv cfv cc0 wceq c0p cidp co wne c1 a1i adantl syl cvv wfn
      cmin vx vy cply csn cdif wrex caa qcn cxp cof wss qsscn cz 1z ax-mp plyid
      zq mp2an plyconst mpan wa caddc qaddcl cmul qmulcl qnegcl plysub peano2cn
      cneg cid cres fnresi df-idp fneq1i mpbir wf fconstg ffn cnex inidm fveq1i
      fvresi syl5eq fvconst2g ofval mpdan ax-1cn pncan2 sylancl ax-1ne0 eqnetrd
      eqtrd ne0p syl2anc eldifsn sylanbrc subid fveq1 eqeq1d rcla4ev elqaa ) AC
      DZAEDZABFZGZHIZBCUCGZJUDUEZUFZAUGDAUHZXBKEAUDZUIZTUJLZXHDZAXMGZHIZXIXBXMX
      GDXMJMZXNXBUAUBCKXLKXGDZXBCEUKZNCDZXRULNUMDXTUNNUQUOZCUPUROXSXBXLXGDULACU
      SUTUAFZCDUBFZCDVAZYBYCVBLCDXBYBYCVCPYDYBYCVDLCDXBYBYCVEPNVICDZXBXTYEYANVF
      UOOVGXBANVBLZEDZYFXMGZHMXQXBXCYGXJAVHQZXBYHNHXBYHYFATLZNXBYGYHYJIYIXBEEYF
      ATEKXLRRYFKESZXBYKVJEVKZESEVLEKYLVMVNVOOZXBEXKXLVPXLESEACVQEXKXLVRQZERDXB
      VSOZYOEVTZYGYFKGZYFIXBYGYQYFYLGYFYFKYLVMWAEYFWBWCPEAYFCWDWEWFXBXCNEDYJNIX
      JWGANWHWIWLNHMXBWJOWKYFXMWMWNXMXGJWOWPXBXOAATLZHXBXCXOYRIXJXBEEAATEKXLRRA
      YMYNYOYOYPXCAKGZAIXBXCYSAYLGAAKYLVMWAEAWBWCPEAACWDWEWFXBXCYRHIXJAWQQWLXFX
      PBXMXHXDXMIXEXOHAXDXMWRWSWTWNABXAWP $.
      $( [23-Jul-2014] $)

    $( The rational numbers are contained in the algebraic numbers.
       (Contributed by Mario Carneiro, 23-Jul-2014.) $)
    qssaa $p |- QQ C_ AA $=
      ( vx cq caa cv qaa ssriv ) ABCADEF $.
      $( [23-Jul-2014] $)

    $( The imaginary unit is algebraic.  (Contributed by Mario Carneiro,
       23-Jul-2014.) $)
    iaa $p |- _i e. AA $=
      ( vf vz ci wcel cc cv cfv cc0 wceq cz c0p c2 cexp co c1 caddc cmpt ax-1cn
      wtru a1i vx vy caa cply csn cdif wrex elaa ax-icn wne eldifsn cxp cof cvv
      cnex sqcl adantl wa eqidd fconstmpt offval2 wss cn0 zsscn 1z plypow mp3an
      2nn0 plyconst mp2an zaddcl plyadd eqeltrrd trud 0cn oveq1d addid2i syl6eq
      sq0i eqid elexi fvmpt ax-mp ax-1ne0 eqnetri ne0p mpbir2an oveq1 i2 negcli
      cneg addcomi negidi eqtri fveq1 eqeq1d rcla4ev ) CUCDCEDZCAFZGZHIZAJUDGZK
      UEUFZUGZCAUHUIBEBFZLMNZOPNZQZXCDZCXHGZHIZXDXIXHXBDZXHKUJZXHXBKUKXLSBEXFQZ
      EOUEULZPUMNXHXBSBEXFOPXNXOUNEEEUNDSUOTXEEDZXFEDSXEUPUQOEDSXPURRTSXNUSXOBE
      OQISBEOUTTVASUAUBJXNXOXNXBDZSJEVBZOJDZLVCDXQVDVEVHBJLVFVGTXOXBDZSXRXSXTVD
      VEOJVIVJTUAFZJDUBFZJDURYAYBPNJDSYAYBVKUQVLVMVNHEDZHXHGZHUJXMVOYDOHYCYDOIV
      OBHXGOEXHXEHIZXGHOPNOYEXFHOPXEVSVPORVQVRXHVTZOERWAWBWCWDWEHXHWFVJWGWRXKUI
      BCXGHEXHXECIZXGOWKZOPNZHYGXFYHOPYGXFCLMNYHXECLMWHWIVRVPYIOYHPNHYHOORWJRWL
      ORWMWNVRYFHEVOWAWBWCXAXKAXHXCWSXHIWTXJHCWSXHWOWPWQVJWG $.
      $( [23-Jul-2014] $)

    $( The reciprocal of an algebraic number is algebraic.  (Contributed by
       Mario Carneiro, 24-Jul-2014.) $)
    aareccl $p |- ( ( A e. AA /\ A =/= 0 ) -> ( 1 / A ) e. AA ) $=
      ( vk vn wcel cc0 wne wa cfv wceq cz c0p cdiv adantr cexp cmul cn0 syl2anc
      co cc syl vf vg vz caa cv cply cdif wrex c1 elaa simprbi aacn reccl sylan
      csn cdgr cfz cmin ccoe csu cmpt wss zsscn a1i simprl eldifsn sylib simpld
      dgrcl wf 0z eqid coef2 sylancl fznn0sub ffvelrn elplyd 0cn cle wbr coefv0
      cif eqidd coeeq2 fveq1d 0nn0 breq1 oveq2 fveq2d ifbieq12d fvex elexi ifex
      fvmpt ax-mp nn0ge0 iftrue 3syl nn0cn subid1 eqtrd syl5eq 3eqtrd simprd wb
      zcn dgreq0 necon3bid mpbid eqnetrd ne0p sylancr sylanbrc oveq2d sumeq2sdv
      oveq1 sumex caddc coef3 elfznn0 adantl ad2antrr expcl syl2an mulcl simplr
      nn0z expne0i syl3anc divcl oveq12d oveq1d fsumrev2 addid2 expsub syl22anc
      fveq2 divass syl112anc divid divdiv32 exprec 3eqtr4d coeid2 simprr eqtr3d
      syl122anc sumeq2dv fzfid fsumdivc div0 3eqtr3d 3eqtr2d fveq1 rcla4ev expr
      eqeq1d rexlimdva mpd ) AUDDZAEFZGZAUAUEZHZEIZUAJUFHZKUOUGZUHZUIALRZUDDZUU
      TUVHUVAUUTASDZUVHAUAUJUKMUVBUVEUVJUAUVGUVBUVCUVGDZUVEUVJUVBUVLUVEGZGZUVIS
      DZUVIUBUEZHZEIZUBUVGUHZUVJUVBUVOUVMUUTUVKUVAUVOAULZAUMUNMZUVNUCSEUVCUPHZU
      QRZUWBBUEZURRZUVCUSHZHZUCUEZUWDNRZORZBUTZVAZUVGDZUVIUWLHZEIZUVSUVNUWLUVFD
      ZUWLKFZUWMUVNUCUWGJBUWBJSVBUVNVCVDUVNUVCUVFDZUWBPDZUVNUWRUVCKFZUVNUVLUWRU
      WTGUVBUVLUVEVEUVCUVFKVFVGZVHZJUVCVITZUVNUWDUWCDZGZPJUWFVJZUWEPDZUWGJDZUXE
      UWREJDZUXFUVNUWRUXDUXBMVKUWFJUVCUWFVLZVMVNUVNUWSUXDUXGUXCPUWDEUWBVOUNPJUW
      EUWFVPQZVQZUVNESDEUWLHZEFUWQVRUVNUXMUWBUWFHZEUVNUXMEUWLUSHZHZEBPUWDUWBVSV
      TZUWGEWBZVAZHZUXNUVNUWPUXMUXPIUXLUXOJUWLUXOVLWATUVNEUXOUXSUVNUCUWGJBUWLUW
      BUXLUXCUXEUXHUWGSDZUXKUWGXFTZUVNUWLWCWDWEUVNUXTEUWBVSVTZUWBEURRZUWFHZEWBZ
      UXNEPDUXTUYFIWFBEUXRUYFPUXSUWDEIZUXQUYCUWGEUYEEUWDEUWBVSWGUYGUWEUYDUWFUWD
      EUWBURWHWIUYGEWCWJUXSVLUYCUYEEUYDUWFWKESVRWLWMWNWOUVNUYFUYEUXNUVNUWSUYCUY
      FUYEIUXCUWBWPUYCUYEEWQWRUVNUYDUWBUWFUVNUWSUWBSDZUYDUWBIUXCUWBWSZUWBWTWRWI
      XAXBXCUVNUWTUXNEFUVNUWRUWTUXAXDUVNUVCKUXNEUVNUWRUVCKIUXNEIXEUXBUWFJUVCUWB
      UWBVLZUXJXGTXHXIXJEUWLXKXLUWLUVFKVFXMUVNUWNUWCUWGUVIUWDNRZORZBUTZUWCCUEZU
      WFHZAUYNNRZORZAUWBNRZLRZCUTZEUVNUVOUWNUYMIUWAUCUVIUWKUYMSUWLUWHUVIIZUWCUW
      JUYLBVUAUWIUYKUWGOUWHUVIUWDNXPXNXOUWLVLUWCUYLBXQWNTUVNUYTUWCEUWBXRRZUWDUR
      RZUWFHZAVUCNRZORZUYRLRZBUTUYMUVNUYSVUGCBEUWBUXIUVNVKVDUVNUWSUWBJDZUXCUWBY
      GTZUVNUYNUWCDZGZUYQSDZUYRSDZUYREFZUYSSDVUKUYOSDZUYPSDZVULVUKPSUWFVJZUYNPD
      ZVUOUVNVUQVUJUVNUWRVUQUXBUWFJUVCUXJXSTMVUJVURUVNUYNUWBXTZYAPSUYNUWFVPQUVN
      UVKVURVUPVUJUUTUVKUVAUVMUVTYBZVUSAUYNYCYDUYOUYPYEQZUVNVUMVUJUVNUVKUWSVUMV
      UTUXCAUWBYCQZMUVNVUNVUJUVNUVKUVAVUHVUNVUTUUTUVAUVMYFZVUIAUWBYHYIZMUYQUYRY
      JYIUYNVUCIZUYQVUFUYRLVVEUYOVUDUYPVUEOUYNVUCUWFYQUYNVUCANWHYKYLYMUVNUWCVUG
      UYLBUXEVUGUWGUYRAUWDNRZLRZORZUYRLRZUWGVVGUYRLRZORZUYLUXEVUFVVHUYRLUXEVUDU
      WGVUEVVGOUXEVUCUWEUWFUXEVUBUWBUWDURUXEUYHVUBUWBIUVNUYHUXDUVNUWSUYHUXCUYIT
      MUWBYNTYLZWIUXEVUEAUWENRZVVGUXEVUCUWEANVVLXNUXEUVKUVAVUHUWDJDZVVMVVGIUVNU
      VKUXDVUTMZUVNUVAUXDVVCMZUVNVUHUXDVUIMUXEUWDPDZVVNUXDVVQUVNUWDUWBXTYAZUWDY
      GTZAUWBUWDYOYPXAYKYLUXEUYAVVGSDZVUMVUNVVIVVKIUYBUXEVUMVVFSDZVVFEFZVVTUVNV
      UMUXDVVBMZUXEUVKVVQVWAVVOVVRAUWDYCQZUXEUVKUVAVVNVWBVVOVVPVVSAUWDYHYIZUYRV
      VFYJYIVWCUVNVUNUXDVVDMZUWGVVGUYRYRYSUXEVVJUYKUWGOUXEUYRUYRLRZVVFLRZUIVVFL
      RZVVJUYKUXEVWGUIVVFLUXEVUMVUNVWGUIIVWCVWFUYRYTQYLUXEVUMVWAVWBVUMVUNVVJVWH
      IVWCVWDVWEVWCVWFUYRVVFUYRUUAUUGUXEUVKUVAVVNUYKVWIIVVOVVPVVSAUWDUUBYIUUCXN
      XCUUHXAUVNUWCUYQCUTZUYRLREUYRLRZUYTEUVNVWJEUYRLUVNUVDVWJEUVNUWRUVKUVDVWJI
      UXBVUTUWFJCUVCUWBAUXJUYJUUDQUVBUVLUVEUUEUUFYLUVNUWCUYQUYRCUVNEUWBUUIVVBVV
      AVVDUUJUVNVUMVUNVWKEIVVBVVDUYRUUKQUULUUMUVRUWOUBUWLUVGUVPUWLIUVQUWNEUVIUV
      PUWLUUNUUQUUOQUVIUBUJXMUUPUURUUS $.
      $( [24-Jul-2014] $)

    $( The conjugate of an algebraic number is algebraic.  (Contributed by
       Mario Carneiro, 24-Jul-2014.) $)
    aacjcl $p |- ( A e. AA -> ( * ` A ) e. AA ) $=
      ( vf cc wcel cv cfv cc0 wceq cz cply c0p csn cdif wrex wa ccj caa cr elaa
      wss adantr fveq2 cj0 syl6eq difss zssre ax-resscn plyss mp2an sstri sseli
      cjcl id plyrecj syl2anr eqeq1d syl5ib reximdva imp jca 3imtr4i ) ACDZABEZ
      FZGHZBIJFZKLZMZNZOZAPFZCDZVKVCFZGHZBVHNZOAQDVKQDVJVLVOVBVLVIAULUAVBVIVOVB
      VEVNBVHVEVDPFZGHVBVCVHDZOZVNVEVPGPFGVDGPUBUCUDVRVPVMGVQVCRJFZDVBVPVMHVBVH
      VSVCVHVFVSVFVGUEIRTRCTVFVSTUFUGIRUHUIUJUKVBUMAVCUNUOUPUQURUSUTABSVKBSVA
      $.
      $( [24-Jul-2014] $)

    $(
    @( The algebraic numbers are countable. @)
    aannen @p |- AA ~~ NN @=
      ? @.

    aaliou.1 @e |- N = ( deg ` F ) @.
    aaliou.2 @e |- ( ph -> F e. ( Poly ` ZZ ) ) @.
    aaliou.3 @e |- ( ph -> N e. NN ) @.
    aaliou.4 @e |- ( ph -> A e. CC ) @.
    aaliou.5 @e |- ( ph -> ( F ` A ) = 0 ) @.
    @( Liouville's theorem on diophantine approximation: Any algebraic number,
       being a root of a polynomial ` F ` in integer coefficients, is not
       approximable beyond order ` N = deg ( F ) ` by rational numbers. In this
       form, it also applies to rational numbers themselves, which are not well
       approximable by other rational numbers. @)
    aaliou @p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN ( A = ( p / q ) \/
      ( x / ( q ^ N ) ) < ( abs ` ( A - ( p / q ) ) ) ) ) @=
      ? @.
    $)
  $}

