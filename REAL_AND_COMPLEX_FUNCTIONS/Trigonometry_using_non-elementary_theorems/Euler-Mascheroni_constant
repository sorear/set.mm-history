$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Euler-Mascheroni constant
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c gamma $. $( Euler-Mascheroni constant (small letter gamma) $)

  $( The Euler-Mascheroni constant.  (The label abbreviates
     Euler-Mascheroni.) $)
  cem $a class gamma $.

  ${
    $( Define the Euler-Macheroni constant, ` gamma = ` 0.577... .  This is the
       limit of the series ` sum_ k e. ( 1 ... m ) ( 1 / k ) - ( log `` m ) ` ,
       with a proof that the limit exists in ~ emcl . $)
    df-em $a |- gamma = sum_ k e. NN
      ( ( 1 / k ) - ( log ` ( 1 + ( 1 / k ) ) ) ) $.
  $}

  ${
    $d k x F $.  $d k x G $.  $d k m H $.  $d m n N $.  $d k m n x T $.
    emcl.1 $e |- F = ( n e. NN |->
      ( sum_ m e. ( 1 ... n ) ( 1 / m ) - ( log ` n ) ) ) $.
    emcl.2 $e |- G = ( n e. NN |->
      ( sum_ m e. ( 1 ... n ) ( 1 / m ) - ( log ` ( n + 1 ) ) ) ) $.
    $( Lemma for ~ emcl .  The series ` F ` and ` G ` are sequences of real
       numbers that approach ` gamma ` from above and below, respectively. $)
    emcllem1 $p |- ( F : NN --> RR /\ G : NN --> RR ) $=
      ( cn cr wf c1 cv co clog cfv cmin wcel wral syl crp nnrp cfz csu fzfid wa
      cdiv elfznn nnrecre fsumrecl relogcl resubcl syl2anc rgen fmpt mpbi caddc
      adantl peano2nn 3syl pm3.2i ) GHCIZGHDIZJBKZUALZJAKZUELZAUBZVBMNZOLZHPZBG
      QUTVIBGVBGPZVFHPZVGHPZVIVJVCVEAVJJVBUCVJVDVCPZUDVDGPZVEHPVMVNVJVDVBUFUPVD
      UGRUHZVJVBSPVLVBTVBUIRVFVGUJUKULBGHVHCEUMUNVFVBJUOLZMNZOLZHPZBGQVAVSBGVJV
      KVQHPZVSVOVJVPGPVPSPVTVBUQVPTVPUIURVFVQUJUKULBGHVRDFUMUNUS $.
      $( [11-Jul-2014] $)

    $( Lemma for ~ emcl . ` F ` is increasing, and ` G ` is decreasing. $)
    emcllem2 $p |- ( N e. NN -> ( ( F ` ( N + 1 ) ) <_ ( F ` N ) /\
                     ( G ` N ) <_ ( G ` ( N + 1 ) ) ) ) $=
      ( wcel c1 caddc co cfv cle wbr cdiv clog cmin cr syl wceq cn cfz peano2nn
      cv csu ce nnrecre crp cc0 nnrp rpreccl rpge0 clt ax-1cn div1i 1re ltaddrp
      sylancr cc nncn addcom breqtrd syl5eqbr wb nnre nngt0 lt01 ltrec1 mpanl12
      syl2anc mpbid eflegeo wne nnne0 recnd rpne0 recdiv syl22anc a1i divsubdir
      wa syl112anc pncan sylancl oveq1d 3eqtr3rd oveq2d rpdivcl reeflog 3eqtr4d
      divid relogdiv relogcl resubcl eqeltrd efle mpbird elfznn adantl fsumrecl
      fzfid leadd2 syl3anc cuz id nnuz syl6eleq oveq2 fsump1 addsub12 lesubadd2
      3brtr4d sumeq1d fveq2 oveq12d ovex fvmpt divdir eqtr2d fveq2d 1rp rpaddcl
      eqtrd efgt1p wi rpre reefcl ltle eqbrtrd eqbrtrrd subadd23 leaddsub oveq1
      mpd jca ) EUAHZEIJKZCLZECLZMNEDLZYQDLZMNYPIYQUBKZIAUDZOKZAUEZYQPLZQKZIEUB
      KZUUDAUEZEPLZQKZYRYSMYPUUGUUKMNZUUEUUFUUKJKZMNZYPUUIIYQOKZJKZUUIUUFUUJQKZ
      JKZUUEUUMMYPUUOUUQMNZUUPUURMNZYPUUOYQEOKZPLZUUQMYPUUOUVBMNZUUOUFLZUVBUFLZ
      MNZYPUVDIIUUOQKZOKZUVEMYPUUOYPYQUAHZUUORHZEUCZYQUGSZYPUUOUHHZUIUUOMNYPYQU
      HHZUVMYPUVIUVNUVKYQUJSZYQUKSZUUOULSYPIIOKZYQUMNZUUOIUMNZYPUVQIYQUMIUNUOYP
      IIEJKZYQUMYPIRHZEUHHZIUVTUMNUPEUJZIEUQURYPIUSHZEUSHZUVTYQTUNEUTZIEVAURVBV
      CYPYQRHZUIYQUMNZUVRUVSVDZYPUVIUWGUVKYQVESZYPUVIUWHUVKYQVFSUWAUIIUMNUWGUWH
      WAUWIUPVGIYQVHVIVJVKVLYPIEYQOKZOKZUVAUVHUVEYPUWEEUIVMYQUSHZYQUIVMZUWLUVAT
      UWFEVNYPYQUWJVOZYPUVNUWNUVOYQVPSZEYQVQVRYPUVGUWKIOYPYQIQKZYQOKZYQYQOKZUUO
      QKZUWKUVGYPUWMUWDUWMUWNUWRUWTTUWOUWDYPUNVSZUWOUWPYQIYQVTWBYPUWQEYQOYPUWEU
      WDUWQETUWFUNEIWCWDWEYPUWSIUUOQYPUWMUWNUWSITUWOUWPYQWKVJZWEWFWGYPUVAUHHZUV
      EUVATYPUVNUWBUXCUVOUWCYQEWHVJUVAWISWJVBYPUVJUVBRHUVCUVFVDUVLYPUVBUUQRYPUV
      NUWBUVBUUQTUVOUWCYQEWLVJZYPUUFRHZUUJRHZUUQRHZYPUVNUXEUVOYQWMSZYPUWBUXFUWC
      EWMSZUUFUUJWNVJZWOUUOUVBWPVJWQUXDVBYPUVJUXGUUIRHZUUSUUTVDUVLUXJYPUUHUUDAY
      PIEXAYPUUCUUHHZWAUUCUAHZUUDRHZUXLUXMYPUUCEWRWSUUCUGZSWTZUUOUUQUUIXBXCVKYP
      UUDUUOAIEYPEUAIXDLYPXEXFXGYPUUCUUBHZWAZUUDUXRUXMUXNUXQUXMYPUUCYQWRWSUXOSZ
      VOUUCYQIOXHXIZYPUUFUSHZUUIUSHZUUJUSHUUMUURTYPUUFUXHVOZYPUUIUXPVOZYPUUJUXI
      VOUUFUUIUUJXJXCXLYPUUERHZUXEUUKRHZUULUUNVDYPUUBUUDAYPIYQXAUXSWTZUXHYPUXKU
      XFUYFUXPUXIUUIUUJWNVJUUEUUFUUKXKXCWQYPUVIYRUUGTUVKBYQIBUDZUBKZUUDAUEZUYHP
      LZQKZUUGUACUYHYQTZUYJUUEUYKUUFQUYMUYIUUBUUDAUYHYQIUBXHXMZUYHYQPXNXOFUUEUU
      FQXPXQSBEUYLUUKUACUYHETZUYJUUIUYKUUJQUYOUYIUUHUUDAUYHEIUBXHXMZUYHEPXNXOFU
      UIUUJQXPXQXLYPUUIUUFQKZUUEYQIJKZPLZQKZYTUUAMYPUYQUYSJKZUUEMNZUYQUYTMNZYPU
      UIUYSUUFQKZJKZUUPVUAUUEMYPVUDUUOMNZVUEUUPMNZYPIUUOJKZPLZVUDUUOMYPVUIUYRYQ
      OKZPLZVUDYPVUHVUJPYPVUJUWSUUOJKZVUHYPUWMUWDUWMUWNVUJVULTUWOUXAUWOUWPYQIYQ
      XRWBYPUWSIUUOJUXBWEXSXTYPUYRUHHZUVNVUKVUDTYPUYRUAHZVUMYPUVIVUNUVKYQUCSUYR
      UJSZUVOUYRYQWLVJYCZYPVUIUUOMNZVUIUFLZUVDMNZYPVURVUHUVDMYPVUHUHHZVURVUHTYP
      IUHHUVMVUTYAUVPIUUOYBURZVUHWISYPVUHUVDUMNZVUHUVDMNZYPUVMVVBUVPUUOYDSYPVUH
      RHZUVDRHZVVBVVCYEYPVUTVVDVVAVUHYFSYPUVJVVEUVLUUOYGSVUHUVDYHVJYNYIYPVUIRHU
      VJVUQVUSVDYPVUIVUDRVUPYPUYSRHZUXEVUDRHZYPVUMVVFVUOUYRWMSZUXHUYSUUFWNVJZWO
      UVLVUIUUOWPVJWQYJYPVVGUVJUXKVUFVUGVDVVIUVLUXPVUDUUOUUIXBXCVKYPUYBUYAUYSUS
      HVUAVUETUYDUYCYPUYSVVHVOUUIUUFUYSYKXCUXTXLYPUYQRHZVVFUYEVUBVUCVDYPUXKUXEV
      VJUXPUXHUUIUUFWNVJVVHUYGUYQUYSUUEYLXCVKBEUYJUYHIJKZPLZQKZUYQUADUYOUYJUUIV
      VLUUFQUYPUYOVVKYQPUYHEIJYMXTXOGUUIUUFQXPXQYPUVIUUAUYTTUVKBYQVVMUYTUADUYMU
      YJUUEVVLUYSQUYNUYMVVKUYRPUYHYQIJYMXTXOGUUEUYSQXPXQSXLYO $.
      $( [11-Jul-2014] $)

    emcl.3 $e |- H = ( n e. NN |-> ( log ` ( 1 + ( 1 / n ) ) ) ) $.
    $( Lemma for ~ emcl .  The function ` H ` is the difference between ` F `
       and ` G ` . $)
    emcllem3 $p |- ( N e. NN -> ( H ` N ) = ( ( F ` N ) - ( G ` N ) ) ) $=
      ( cn wcel c1 cdiv co caddc clog cfv cmin wceq cc cfz cv csu peano2nn nnrp
      crp syl relogdiv syl2anc cc0 wne nncn ax-1cn nnne0 divdir syl112anc divid
      a1i oveq1d eqtr2d fveq2d fzfid wa cr elfznn adantl nnrecre fsumrecl recnd
      relogcl nnncan1 syl3anc 3eqtr4d oveq2 fvex fvmpt sumeq1d fveq2 ovex oveq1
      oveq2d oveq12d ) FJKZLLFMNZONZPQZLFUANZLAUBZMNZAUCZFPQZRNZWJFLONZPQZRNZRN
      ZFEQFCQZFDQZRNWCWMFMNZPQZWNWKRNZWFWPWCWMUFKZFUFKZWTXASWCWMJKXBFUDWMUEUGZF
      UEZWMFUHUIWCWEWSPWCWSFFMNZWDONZWEWCFTKZLTKZXHFUJUKZWSXGSFULZXIWCUMURXKFUN
      ZFLFUOUPWCXFLWDOWCXHXJXFLSXKXLFUQUIUSUTVAWCWJTKWKTKWNTKWPXASWCWJWCWGWIAWC
      LFVBWCWHWGKZVCWHJKZWIVDKXMXNWCWHFVEVFWHVGUGVHVIWCWKWCXCWKVDKXEFVJUGVIWCWN
      WCXBWNVDKXDWMVJUGVIWJWKWNVKVLVMBFLLBUBZMNZONZPQWFJEXOFSZXQWEPXRXPWDLOXOFL
      MVNWAVAIWEPVOVPWCWQWLWRWORBFLXOUANZWIAUCZXOPQZRNWLJCXRXTWJYAWKRXRXSWGWIAX
      OFLUAVNVQZXOFPVRWBGWJWKRVSVPBFXTXOLONZPQZRNWOJDXRXTWJYDWNRYBXRYCWMPXOFLOV
      TVAWBHWJWNRVSVPWBVM $.
      $( [11-Jul-2014] $)

    $( Lemma for ~ emcl .  The difference between series ` F ` and ` G ` tends
       to zero. $)
    emcllem4 $p |- H ~~> 0 $=
      ( c1 wcel cc0 wbr cn cdiv clog cfv cr crp clt syl cc cli ax-1cn cv co cvv
      cmpt nnuz cz 1z a1i 0cn divcnv caddc nnex mptex eqeltri wa wceq eqid ovex
      oveq2 fvmpt adantl nnrecre eqeltrd oveq2d fveq2d fvex 1rp rpreccl rpaddcl
      nnrp sylancr rpre 1re ltaddrp rplogcl syl2anc cle ce efgt1p rpefcl logltb
      wb mpbid relogef breqtrd wi relogcl ltle mpd 3brtr4d rpge0 climsqz2 ax-mp
      ) IUAJZEKUBLUCWQKABMIBUDZNUEZUGZEIUAUFMUHIUIJWQUJUKKUAJWQULUKIBUMEUFJWQEB
      MIWSUNUEZOPZUGUFHBMXBUOUPUQUKWQAUDZMJZURZXCWTPZIXCNUEZQXDXFXGUSWQBXCWSXGM
      WTWRXCINVBZWTUTIXCNVAVCVDZXDXGQJZWQXCVEVDZVFXEXCEPZRJZXLQJXEXLIXGUNUEZOPZ
      RXDXLXOUSWQBXCXBXOMEWRXCUSZXAXNOXPWSXGIUNXHVGVHHXNOVIVCVDZXEXNQJZIXNSLZXO
      RJXEXNRJZXRXEIRJXGRJZXTVJXEXCRJZYAXDYBWQXCVMVDXCVKTZIXGVLVNZXNVOTXEIQJYAX
      SVPYCIXGVQVNXNVRVSVFZXLVOTXEXOXGXLXFVTXEXOXGSLZXOXGVTLZXEXOXGWAPZOPZXGSXE
      XNYHSLZXOYISLZXEYAYJYCXGWBTXEXTYHRJZYJYKWEYDXEXJYLXKXGWCTXNYHWDVSWFXEXJYI
      XGUSXKXGWGTWHXEXOQJZXJYFYGWIXEXTYMYDXNWJTXKXOXGWKVSWLXQXIWMXEXMKXLVTLYEXL
      WNTWOWP $.
      $( [11-Jul-2014] $)

    emcl.4 $e |- T = ( n e. NN |->
                       ( ( 1 / n ) - ( log ` ( 1 + ( 1 / n ) ) ) ) ) $.
    $( Lemma for ~ emcl .  The partial sums of the series ` T ` , which is used
       in the definition ~ df-em , is in fact the same as ` G ` . $)
    emcllem5 $p |- G = seq 1 ( + , T ) $=
      ( cn c1 co caddc clog cfv cmin wcel syl crp vx cv cfz cdiv csu cmpt fzfid
      cseq wa elfznn adantl nnrecre recnd 1rp nnrp rpreccl 3syl rpaddcl sylancr
      cr relogcl fsumsub wceq oveq2 oveq2d fveq2d oveq12d fvmpt cuz id syl6eleq
      ovex nnuz resubcl syl2anc fsumser cc cc0 wne nncn ax-1cn a1i nnne0 divdir
      syl112anc divid oveq1d eqtrd peano2nn relogdiv eqtr3d sumeq2dv fsumtscop2
      fveq2 nnz log1 oveq2i subid1 syl5eq 3eqtrd 3eqtr3rd mpteq2ia wfn cz seqfn
      1z ax-mp fneq2i mpbir dffn5v mpbi 3eqtr4i ) CKLCUBZUCMZLBUBZUDMZBUEZXMLNM
      ZOPZQMZUFCKXMNALUHZPZUFZEYACKXTYBXMKRZXNXPLXPNMZOPZQMZBUEXQXNYFBUEZQMYBXT
      YDXNXPYFBYDLXMUGYDXOXNRZUIZXPYJXOKRZXPUTRZYIYKYDXOXMUJUKZXOULSZUMYJYFYJYE
      TRZYFUTRZYJLTRXPTRZYOUNYJYKXOTRZYQYMXOUOZXOUPUQLXPURUSYEVASZUMVBYDYGBALXM
      YJYKXOAPYGVCYMCXOLXMUDMZLUUANMZOPZQMYGKAXMXOVCZUUAXPUUCYFQXMXOLUDVDZUUDUU
      BYEOUUDUUAXPLNUUEVEVFVGJXPYFQVLVHSYDXMKLVIPZYDVJVMVKYJYGYJYLYPYGUTRYNYTXP
      YFVNVOUMVPYDYHXSXQQYDYHXNXOLNMZOPZXOOPZQMZBUEXSLOPZQMZXSYDXNYFUUJBYJUUGXO
      UDMZOPZYFUUJYJUUMYEOYJUUMXOXOUDMZXPNMZYEYJXOVQRZLVQRZUUQXOVRVSZUUMUUPVCYJ
      YKUUQYMXOVTSZUURYJWAWBUUTYJYKUUSYMXOWCSZXOLXOWDWEYJUUOLXPNYJUUQUUSUUOLVCU
      UTUVAXOWFVOWGWHVFYJUUGTRZYRUUNUUJVCYJYKUUGKRUVBYMXOWIUUGUOUQYJYKYRYMYSSUU
      GXOWJVOWKWLYDUAUBZOPZUUIUUHUUKBUAXSLXMUVCXOOWNUVCUUGOWNUVCLOWNUVCXROWNXMW
      OYDXRKUUFXMWIZVMVKYDUVCLXRUCMRZUIZUVDUVGUVCKRZUVCTRUVDUTRUVFUVHYDUVCXRUJU
      KUVCUOUVCVAUQUMWMYDUULXSVRQMZXSUUKVRXSQWPWQYDXSVQRUVIXSVCYDXSYDXRKRXRTRXS
      UTRUVEXRUOXRVAUQUMXSWRSWSWTVEXAXBHYAKXCZYAYCVCUVJYAUUFXCZLXDRUVKXFNALXEXG
      KUUFYAVMXHXICKYAXJXKXL $.
      $( [11-Jul-2014] $)

    $( Lemma for ~ emcl .  By the previous lemmas, ` F ` and ` G ` must
       approach a common limit, which is ` gamma ` by definition. $)
    emcllem6 $p |- ( F ~~> gamma /\ G ~~> gamma ) $=
      ( vk cem wbr wtru co c1 cn wcel cr cle vx cli cc0 caddc cvv cc nnuz cz 1z
      a1i cv cdiv clog cfv cmin csu df-em sumex eqeltri cseq wceq oveq2d fveq2d
      oveq2 oveq12d ovex adantl wa nnrecre crp 1rp nnrp rpreccl rpaddcl sylancr
      fvmpt syl relogcl resubcl syl2anc recnd crn clt csup emcllem5 wf emcllem1
      cdm simpri emcllem2 simprd wral 1nn simpli ffvelrni ax-mp emcllem3 eqtr3d
      wrex 1re readdcl ltaddrp rplogcl eqeltrrd rpge0 subge0 mpbid fveq2 breq1d
      fvex wb leidi simpld wi peano2nn letr syl3anc mpand nnind letrd ralrimiva
      breq2 ralbidv rcla4ev climsup syl5eqbrr breldm isumclim2 3brtr4g 0cn cmpt
      seqex cfz nnex mptex emcllem4 eqeltrd pncan3 eqtr2d trud climadd syl6breq
      climcl mp2an addid1i pm3.2i ) DLUBMZELUBMZUUGNDLUCUDOLUBNLUCKEFDPUEUFUEQU
      GPUHRNUIUJZLUERZNLQPKUKZULOZPUULUDOZUMUNZUOOZKUPZUEKUQZQUUOKURUSZUJNUDAPU
      TZUUPELUBNUUOKAPQUGUUIUUKQRZUUKAUNUUOVANCUUKPCUKZULOZPUVBUDOZUMUNZUOOUUOQ
      AUVAUUKVAZUVBUULUVDUUNUOUVAUUKPULVDZUVEUVCUUMUMUVEUVBUULPUDUVFVBVCZVEJUUL
      UUNUOVFVPVGNUUTVHZUUOUVHUULSRZUUNSRZUUOSRUUTUVINUUKVIVGZUVHUUMVJRZUVJUVHP
      VJRUULVJRZUVLVKUUTUVMNUUTUUKVJRUVMUUKVLUUKVMVQVGZPUULVNVOUUMVRVQUULUUNVSV
      TWANUUSEWBSWCWDZUBMUUSUBWHRNUUSEUVOUBABCDEFGHIJWEZNUAKEPQUGUUIQSEWFZNQSDW
      FZUVQBCDEGHWGZWIZUJUUTUUKEUNZUUKPUDOZEUNTMZNUUTUWBDUNZUUKDUNZTMZUWCBCDEUU
      KGHWJZWKVGNPDUNZSRZUWAUWHTMZKQWLZUWAUAUKZTMZKQWLZUASWSPQRUWIWMQSPDUVRUVQU
      VSWNZWOWPZNUWJKQUVHUWAUWEUWHUUTUWASRZNQSUUKEUVTWOVGZUUTUWESRZNQSUUKDUWOWO
      ZVGZUWIUVHUWPUJUVHUCUWEUWAUOOZTMZUWAUWETMZUVHUXBVJRUXCUVHUUNUXBVJUVHUUKFU
      NZUUNUXBUUTUXEUUNVANCUUKUVDUUNQFUVGIUUMUMXJVPVGUUTUXEUXBVANBCDEFUUKGHIWQV
      GZWRUVHUUMSRZPUUMWCMZUUNVJRUVHPSRZUVIUXGWTUVKPUULXAVOUVHUXIUVMUXHWTUVNPUU
      LXBVOUUMXCVTXDUXBXEVQUVHUWSUWQUXCUXDXKUXAUWRUWEUWAXFVTXGUUTUWEUWHTMZNUWLD
      UNZUWHTMUWHUWHTMUXJUWDUWHTMZUXJUAKUUKUWLPVAUXKUWHUWHTUWLPDXHXIUWLUUKVAUXK
      UWEUWHTUWLUUKDXHXIZUWLUWBVAUXKUWDUWHTUWLUWBDXHXIUXMUWHUWPXLUUTUWFUXJUXLUU
      TUWFUWCUWGXMUUTUWDSRZUWSUWIUWFUXJVHUXLXNUUTUWBQRUXNUUKXOQSUWBDUWOWOVQUWTU
      WIUUTUWPUJUWDUWEUWHXPXQXRXSVGXTYAUWNUWKUAUWHSUWLUWHVAUWMUWJKQUWLUWHUWATYB
      YCYDVOYEYFUUSUVOUBUDAPYLYGVQYHUVPUUQYIZUCUFRNYJUJDUERNDCQPUVAYMOPBUKULOBU
      PUVAUMUNUOOZYKUEGCQUXPYNYOUSUJFUCUBMNBCDEFGHIYPUJUVHUWAUWRWAZUVHUXEUVHUXE
      UXBSUXFUVHUWSUWQUXBSRUXAUWRUWEUWAVSVTYQWAUVHUWAUXEUDOUWAUXBUDOZUWEUVHUXEU
      XBUWAUDUXFVBUVHUWAUFRUWEUFRUXRUWEVAUXQUVHUWEUXAWAUWAUWEYRVTYSUUALUUJUUHLU
      FRUURUUHUXOYTZLEUEUUCUUDUUEUUBYTUXSUUF $.
      $( [11-Jul-2014] $)

    $( Lemma for ~ emcl .  Derive bounds on ` gamma ` as ` F ( 1 ) ` and
       ` G ( 1 ) ` . $)
    emcllem7 $p |- gamma e. ( ( 1 - ( log ` 2 ) ) [,] 1 ) $=
      ( cem c1 cfv co wcel wtru cr cle cn wceq vk vx c2 clog cmin cicc wbr nnuz
      cvv cz 1z a1i cli emcllem6 simpri cv wf emcllem1 ffvelrni adantl cdiv csu
      caddc df-em sumex eqeltri climrecl 1nn cfz oveq2 sumeq1d cc ax-1cn syl6eq
      div1i fsum1 mp2an oveq1 syl6eqr fveq2d oveq12d 1re crp 2re elrpii relogcl
      df-2 2pos ax-mp resubcli elexi emcllem2 simprd climub syl5eqbrr cneg cmpt
      fvmpt cc0 simpli 0cn nnex mptex recnd fveq2 negeqd negex df-neg climsubc2
      wa eqid renegcl syl eqeltrd simpld wb peano2nn leneg syl2anc mpbid eqcomi
      3brtr4d 3brtr4g trud subid1i lenegi sylibr syl6breq elicc2i syl3anbrc
      log1 ) KLUCUDMZUENZLUFNOZPKQOZYMKRUGKLRUGYNPKUAELUISUHLUJOZPUKULZEKUMUGZP
      DKUMUGZYRABCDEFGHIJUNZUOULZUAUPZSOZUUBEMZQOPSQUUBESQDUQZSQEUQZBCDEGHURZUO
      USUTZKUIOPKSLUUBVANZLUUIVCNUDMUENZUAVBUIUAVDSUUJUAVEVFULZVGZPYMLEMZKRLSOZ
      UUMYMTVHCLLCUPZVINZLBUPZVANZBVBZUUOLVCNZUDMZUENYMSEUUOLTZUUSLUVAYLUEUVBUU
      SLLVINZUURBVBZLUVBUUPUVCUURBUUOLLVIVJVKYPLVLOUVDLTUKVMUURLBLUUQLTUURLLVAN
      LUUQLLVAVJLVMVOVNVPVQVNZUVBUUTUCUDUVBUUTLLVCNUCUUOLLVCVRWGVSVTWAHYMQLYLWB
      UCWCOYLQOUCWDWHWEUCWFWIWJZWKWRWIPKUAELLSUHUUNPVHULZUUAUUHUUCUUDUUBLVCNZEM
      RUGZPUUCUVHDMZUUBDMZRUGZUVIBCDEUUBGHWLZWMUTWNWOPKLDMZLRPUVNWPZKWPZRUGKUVN
      RUGPLUBSUBUPZDMZWPZWQZMZWSKUENZUVOUVPRPUWBUAUVTLLSUHUVGPKWSUADUVTLUIUISUH
      YQUUKYSPYSYRYTWTULWSVLOPXAULUVTUIOPUBSUVSXBXCULPUUCXJZUVKUUCUVKQOZPSQUUBD
      UUEUUFUUGWTZUSUTZXDUWCUUBUVTMZUVKWPZWSUVKUENUUCUWGUWHTPUBUUBUVSUWHSUVTUVQ
      UUBTUVRUVKUVQUUBDXEXFUVTXKZUVKXGWRUTZUVKXHVNXIUWCUWGUWHQUWJUWCUWDUWHQOUWF
      UVKXLXMXNUWCUWHUVJWPZUWGUVHUVTMZRUWCUVLUWHUWKRUGZUUCUVLPUUCUVLUVIUVMXOUTU
      WCUVJQOZUWDUVLUWMXPUWCUVHSOZUWNUUCUWOPUUBXQUTZSQUVHDUWEUSXMUWFUVJUVKXRXSX
      TUWJUWCUWOUWLUWKTUWPUBUVHUVSUWKSUVTUVQUVHTUVRUVJUVQUVHDXEXFUWIUVJXGWRXMYB
      WNUWAUVOUUNUWAUVOTVHUBLUVSUVOSUVTUVQLTUVRUVNUVQLDXEXFUWIUVNXGWRWIYAKXHYCK
      UVNYOUULYDUVNLQUUNUVNLTVHCLUUSUUOUDMZUENZLSDUVBUWRLWSUENLUVBUUSLUWQWSUEUV
      EUVBUWQLUDMWSUUOLUDXEYKVNWALVMYEVNGLQWBWKWRWIZWBVFYFYGUWSYHYMLKUVFWBYIYJY
      D $.
      $( [11-Jul-2014] $)
  $}

  ${
    $d k m n $.
    $( Closure and bounds for the Euler-Macheroni constant. $)
    emcl $p |- gamma e. ( ( 1 - ( log ` 2 ) ) [,] 1 ) $=
      ( vk vm vn cn c1 cv cdiv caddc clog cfv cmin cmpt cfz csu eqid wceq oveq2
      co oveq2d fveq2d oveq12d cbvmptv emcllem7 ) ADEAFZGRZEUEHRZIJZKRZLBCCDECF
      ZMREBFGRBNZUIIJKRLZCDUJUIEHRIJKRLZCDEEUIGRZHRZIJZLZUKOULOUPOACDUHUMUOKRUD
      UIPZUEUMUGUOKUDUIEGQZUQUFUNIUQUEUMEHURSTUAUBUC $.
      $( [11-Jul-2014] $)
  $}

  $( The Euler-Macheroni constant is a real number. $)
  emre $p |- gamma e. RR $=
    ( c1 c2 clog cfv cmin co cicc cr cem wcel wss 1re crp 2re 2pos elrpii ax-mp
    relogcl resubcli iccssre mp2an emcl sselii ) ABCDZEFZAGFZHIUEHJAHJUFHKAUDLB
    MJUDHJBNOPBRQSLUEATUAUBUC $.
    $( [11-Jul-2014] $)

  $( The Euler-Macheroni constant is positive. $)
  emgt0 $p |- 0 < gamma $=
    ( cc0 c1 c2 clog cfv cmin co clt wbr cem cle ce ceu c3 egt2lt3 ax-mp cr 1re
    wcel mp2an simpli crp wceq 2re 2pos elrpii reeflog df-e eqcomi 3brtr4i eflt
    wb relogcl posdifi mpbi cicc emcl resubcli elicc2i simp2bi 0re emre ltletri
    mpbir ) ABCDEZFGZHIZVFJKIZAJHIVEBHIZVGVIVELEZBLEZHIZCMVJVKHCMHIMNHIOUACUBSZ
    VJCUCCUDUEUFZCUGPMVKUHUIUJVEQSZBQSVIVLULVMVOVNCUMPZRVEBUKTVDVEBVPRUNUOJVFBU
    PGSZVHUQVQJQSVHJBKIVFBJBVERVPURZRUSUTPAVFJVAVRVBVCT $.
    $( [11-Jul-2014] $)

