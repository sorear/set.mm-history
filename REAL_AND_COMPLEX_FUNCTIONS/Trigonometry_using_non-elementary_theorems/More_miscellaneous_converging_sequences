$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         More miscellaneous converging sequences
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d n x y A $.
    $( A power to a negative exponent goes to zero as the base becomes large.
       (Contributed by Mario Carneiro, 15-Sep-2014.) $)
    cxplim $p |- ( A e. RR+ -> ( n e. RR+ |-> ( n ^c -u A ) ) ~~>r 0 ) $=
      ( crp wcel ccxp co cc0 wbr clt cr c1 cdiv cle rpre syl cc syl2anc syl3anc
      wceq ad2antrr vy vx cv cneg cmpt crli cabs cfv wi wral wrex wa adantl wne
      rpge0 renegcl adantr rpne0 wb rpcn negeq0 necon3bid mpbid rereccl recxpcl
      simprl cxpge0 absid cxpneg eqtrd simprr simplr rpreccl recnd ad2antlr a1i
      cxprec ax-1cn divneg2 oveq2d 3eqtr2d recid cxpmul 3eqtr3d 3brtr4d rpcxpcl
      cmul cxp1 cxplt2 syl221anc mpbird rpgt0 ltrec recrec breqtrd eqbrtrd expr
      syl22anc ralrimiva breq1 imbi1d ralbidv rcla4ev cxpcl syl2anr wss rlim0lt
      rpssre ) ACDZBCBUCZAUDZEFZUEGUFHUAUCZXJIHZXLUGUHZUBUCZIHZUIZBCUJZUAJUKZUB
      CUJXIXTUBCXIXPCDZULZXPKXKLFZEFZJDZYDXJIHZXQUIZBCUJZXTYBXPJDZGXPMHZYCJDZYE
      YAYIXIXPNUMYAYJXIXPUOUMYBXKJDZXKGUNZYKXIYLYAXIAJDZYLANZAUPOZUQXIYMYAXIAGU
      NZYMAURZXIAPDZYQYMUSAUTZYSAGXKGAVAVBOVCUQXKVDQXPYCVERYBYGBCYBXJCDZYFXQYBU
      UAYFULZULZXOKXJAEFZLFZXPIUUCXOXLUUEUUCXLJDZGXLMHZXOXLSUUCXJJDZGXJMHZYLUUF
      UUCUUAUUHYBUUAYFVFZXJNOZUUCUUAUUIUUJXJUOOZXIYLYAUUBYPTZXJXKVERUUCUUHUUIYL
      UUGUUKUULUUMXJXKVGRXLVHQUUCXJPDZXJGUNZYSXLUUESUUCUUAUUNUUJXJUTZOZUUCUUAUU
      OUUJXJUROXIYSYAUUBYTTZXJAVIRVJUUCUUEKKXPLFZLFZXPIUUCUUSUUDIHZUUEUUTIHZUUC
      UVAUUSKALFZEFZUUDUVCEFZIHZUUCYDXJUVDUVEIYBUUAYFVKUUCUVDKXPUVCEFLFZXPUVCUD
      ZEFZYDUUCYAUVCPDZUVDUVGSXIYAUUBVLUUCUVCUUCUVCCDZUVCJDXIUVKYAUUBAVMTZUVCNO
      VNZXPUVCVQQUUCXPPDZXPGUNZUVJUVIUVGSYAUVNXIUUBXPUTVOZYAUVOXIUUBXPURVOZUVMX
      PUVCVIRUUCUVHYCXPEUUCKPDZYSYQUVHYCSUVRUUCVRVPUURXIYQYAUUBYRTZKAVSRVTWAUUC
      XJAUVCWGFZEFZXJKEFZUVEXJUUCUVTKXJEUUCYSYQUVTKSUURUVSAWBQVTUUCUUAYNUVJUWAU
      VESUUJXIYNYAUUBYOTZUVMXJAUVCWCRUUCUUNUWBXJSUUQXJWHOWDWEUUCUUSJDZGUUSMHZUU
      DJDZGUUDMHZUVKUVAUVFUSUUCUUSCDZUWDYAUWHXIUUBXPVMVOZUUSNOZUUCUWHUWEUWIUUSU
      OOUUCUUDCDZUWFUUCUUAYNUWKUUJUWCXJAWFQZUUDNOZUUCUWKUWGUWLUUDUOOUVLUUSUUDUV
      CWIWJWKUUCUWDGUUSIHZUWFGUUDIHZUVAUVBUSUWJUUCUWHUWNUWIUUSWLOUWMUUCUWKUWOUW
      LUUDWLOUUSUUDWMWRVCUUCUVNUVOUUTXPSUVPUVQXPWNQWOWPWQWSXSYHUAYDJXMYDSZXRYGB
      CUWPXNYFXQXMYDXJIWTXAXBXCQWSXIUBUABCXLUUAUUNXKPDXLPDXIUUPXIXKYPVNXJXKXDXE
      CJXFXIXHVPXGWK $.
      $( [15-Sep-2014] $)
  $}

  ${
    $d n x y A $.  $d x y B $.  $d n x y C $.  $d n x y ph $.
    rlimcxp.1 $e |- ( ( ph /\ n e. A ) -> B e. V ) $.
    rlimcxp.2 $e |- ( ph -> ( n e. A |-> B ) ~~>r 0 ) $.
    rlimcxp.3 $e |- ( ph -> C e. RR+ ) $.
    $( Any power to a positive exponent of a converging sequence also
       converges.  (Contributed by Mario Carneiro, 18-Sep-2014.) $)
    rlimcxp $p |- ( ph -> ( n e. A |-> ( B ^c C ) ) ~~>r 0 ) $=
      ( vy ccxp co cc0 wbr clt cr crp wcel cc syl vx cmpt crli cv cle cabs wral
      cfv wi wrex wa cmin c1 cdiv wf cdm rlimf wceq ralrimiva dmmptg feq2d eqid
      mpbid fmpt sylibr r19.21bi adantlr 0cn a1i simpr adantr rpreccl rpre 3syl
      rpcxpcl syl2anc rlimi abscl absge0 rpge0 ad2antrr cxplt2 syl221anc subid1
      wb fveq2d breq1d abscxp2 cmul wne rpcn rpne0 recid2 oveq2d simplr syl3anc
      cxpmul cxp1 3eqtr3rd breq12d 3bitr4d biimpd imim2d ralimdva reximdv cxpcl
      mpd wss rlimss eqsstr3d rlim0 mpbird ) AEBCDKLZUBMUCNJUDEUDZUENZXMUFUHZUA
      UDZONZUIZEBUGZJPUJZUAQUGAYAUAQAXQQRZUKZXOCMULLZUFUHZXQUMDUNLZKLZONZUIZEBU
      GZJPUJYAYCJEBCMYGSSAXNBRZCSRZYBAYLEBABSEBCUBZUOZYLEBUGAYMUPZSYMUOZYNAYMMU
      CNZYPHMYMUQTAYOBSYMACFRZEBUGYOBURAYREBGUSEBCFUTTZVAVCEBSCYMYMVBVDVEVFZVGZ
      MSRYCVHVIYCYBYFPRZYGQRZAYBVJYCDQRZYFQRUUBAUUDYBIVKDVLYFVMVNZXQYFVOVPZAYQY
      BHVKVQYCYJXTJPYCYIXSEBYCYKUKZYHXRXOUUGYHXRUUGCUFUHZYGONZUUHDKLZYGDKLZONZY
      HXRUUGUUHPRZMUUHUENZYGPRZMYGUENZUUDUUIUULWEUUGYLUUMUUACVRTUUGYLUUNUUACVST
      UUGUUCUUOYCUUCYKUUFVKZYGVMTUUGUUCUUPUUQYGVTTAUUDYBYKIWAZUUHYGDWBWCUUGYEUU
      HYGOUUGYDCUFUUGYLYDCURUUACWDTWFWGUUGXPUUJXQUUKOUUGYLDPRZXPUUJURUUAUUGUUDU
      USUURDVMTCDWHVPUUGXQYFDWILZKLZXQUMKLZUUKXQUUGUUTUMXQKUUGDSRZDMWJZUUTUMURU
      UGUUDUVCUURDWKZTZUUGUUDUVDUURDWLTDWMVPWNUUGYBUUBUVCUVAUUKURAYBYKWOZYCUUBY
      KUUEVKUVFXQYFDWQWPUUGYBXQSRUVBXQURUVGXQWKXQWRVNWSWTXAXBXCXDXEXGUSAUAJEBXM
      AYKUKYLUVCXMSRYTAUVCYKAUUDUVCIUVETVKCDXFVPABYOPYSAYQYOPXHHMYMXITXJXKXL $.
      $( [18-Sep-2014] $)
  $}

  ${
    $d m x y z A $.  $d m y z B $.  $d m x y z C $.  $d m x y z ph $.
    o1cxp.1 $e |- ( ph -> C e. CC ) $.
    o1cxp.2 $e |- ( ph -> 0 <_ ( Re ` C ) ) $.
    o1cxp.3 $e |- ( ( ph /\ x e. A ) -> B e. V ) $.
    o1cxp.4 $e |- ( ph -> ( x e. A |-> B ) e. O(1) ) $.
    $( An eventually bounded function taken to a nonnegative power is
       eventually bounded.  (Contributed by Mario Carneiro, 15-Sep-2014.) $)
    o1cxp $p |- ( ph -> ( x e. A |-> ( B ^c C ) ) e. O(1) ) $=
      ( vy vz cle cfv cr ccxp wcel cc wa cc0 vm cv wbr cmpt cabs wi wral co co1
      wrex wf cdm o1f syl wceq ralrimiva dmmptg feq2d mpbid syl2anc cif cre cpi
      o1bdd cmul ce simpr eqid fvmpt2 oveq1d cvv ovex sylancl eqtr4d ax-17 hbfv
      hbmpt1 hbov hbeq fveq2 eqeq12d cbvral sylib r19.21bi fveq2d ffvelrn sylan
      ad2ant2r ad2antrr simprr 0re ifcl adantr abscl sylancr abscxpbnd eqbrtrrd
      max2 letrd expr imim2d ralimdva wss fmpt sylibr cxpcl fmptd o1dm eqsstr3d
      simprl max1 recl recxpcl syl3anc pire remulcl reefcl elo12r syl22anc syld
      3expia rexlimdvva mpd ) AKUBZLUBZMUCZYEBCDUDZNZUENZUAUBZMUCZUFZLCUGZUAOUJ
      KOUJZBCDEPUHZUDZUIQZAYGUIQZCRYGUKZYNJAYGULZRYGUKZYSAYRUUAJYGUMUNAYTCRYGAD
      FQZBCUGYTCUOAUUBBCIUPBCDFUQUNZURUSZKLCUAYGVDUTAYMYQKUAOOAYDOQZYJOQZSZSZYM
      YFYEYPNZUENZTYJMUCZYJTVAZEVBNZPUHZEUENZVCVEUHZVFNZVEUHZMUCZUFZLCUGZYQUUHY
      LUUTLCUUHYECQZSYKUUSYFUUHUVBYKUUSUUHUVBYKSZSZYHEPUHZUENUUJUURMUVDUVEUUIUE
      AUVBUVEUUIUOZUUGYKAUVFLCABUBZYGNZEPUHZUVGYPNZUOZBCUGUVFLCUGAUVKBCAUVGCQZS
      ZUVIYOUVJUVMUVHDEPUVMUVLUUBUVHDUOAUVLVGZIBCDFYGYGVHZVIUTVJUVMUVLYOVKQUVJY
      OUOUVNDEPVLBCYOVKYPYPVHZVIVMVNUPUVKUVFBLCUVKLVOBKKUVEUUIBKYHEPBKYEYGBKCDV
      QYDYEQBVOZVPYDPQBVOYDEQBVOVRBKYEYPBKCYOVQUVQVPVSUVGYEUOZUVIUVEUVJUUIUVRUV
      HYHEPUVGYEYGVTVJUVGYEYPVTWAWBWCWDWHWEUVDYHEUULAUVBYHRQZUUGYKAYSUVBUVSUUDC
      RYEYGWFWGWHZAERQZUUGUVCGWIATUUMMUCUUGUVCHWIUUHUULOQZUVCUUHUUFTOQZUWBAUUEU
      UFWJZWKUUKYJTOWLVMZWMZUVDYIYJUULUVDUVSYIOQUVTYHWNUNUUHUUFUVCUWDWMUWFUUHUV
      BYKWJUUHYJUULMUCZUVCUUHUWCUUFUWGWKUWDTYJWRWOWMWSWPWQWTXAXBUUHCRYPUKZCOXCZ
      UUEUUROQZUVAYQUFAUWHUUGABCYORYPUVMDRQZUWAYORQAUWKBCAYSUWKBCUGUUDBCRDYGUVO
      XDXEWDAUWAUVLGWMDEXFUTUVPXGWMAUWIUUGACYTOUUCAYRYTOXCJYGXHUNXIWMAUUEUUFXJU
      UHUUNOQZUUQOQZUWJUUHUWBTUULMUCZUUMOQZUWLUWEUUHUWCUUFUWNWKUWDTYJXKWOUUHUWA
      UWOAUWAUUGGWMZEXLUNUULUUMXMXNUUHUUPOQZUWMUUHUUOOQZVCOQUWQUUHUWAUWRUWPEWNU
      NXOUUOVCXPVMUUPXQUNUUNUUQXPUTUWHUWISUUEUWJSUVAYQLCYDYPUURXRYAXSXTYBYC $.
      $( [15-Sep-2014] $)
  $}

  ${
    $d m n x y z A $.  $d n B $.
    $( A linear factor grows slower than any exponential with base greater than
       ` 1 ` .  (Contributed by Mario Carneiro, 15-Sep-2014.) $)
    cxp2limlem $p |- ( ( A e. RR /\ 1 < A ) ->
      ( n e. RR+ |-> ( n / ( A ^c n ) ) ) ~~>r 0 ) $=
      ( cr wcel c1 clt wbr crp c2 cdiv cc0 a1i rpre syl syl2anc adantr wne wceq
      co cc wa clog cfv cexp ccxp 0re cmpt crli 2re 2pos elrpii rplogcl rpexpcl
      cv cz 2z sylancl rpdivcl sylancr recnd divrcnv rerpdivcl sylan simpr lt01
      simpl 1re lttrd elrp sylanbrc rpcxpcl syl2an cmul ce caddc rpmulcl resqcl
      cle rehalfcl 1rp rpaddcl readdcl reefcl rpgt0 wb ltaddpos2 efgt1p2 adantl
      mpbid sqcl 2ne0 rpne0 divdiv2 syl122anc sqmul oveq1d eqtr4d cxpef syl3anc
      2cn rpcn 3brtr4d ltdiv2 syl33anc sqval oveq2d divcan5 3eqtrd breqtrd ltle
      wi mpd rpge0 rlimsqz2 ) ACDZEAFGZUAZBHIAUBUCZIUDSZJSZBUNZJSZYAAYAUESZJSZK
      KCDXQUFLZXQXTTDZBHYBUGKUHGXQXTXQXTHDZXTCDZXQIHDXSHDZYGIUIUJUKXQXRHDZIUODZ
      YIAULZUPXRIUMUQZIXSURUSZXTMNZUTZXTBVANXQYHYAHDZYBCDZYOXTYAVBVCZXQYQUAZYDH
      DZYDCDZYTYQYCHDZUUAXQYQVDZXQAHDZYACDZUUCYQXQXOKAFGUUEXOXPVFZXQKEAYEECDXQV
      GLUUGKEFGXQVELXOXPVDVHAVIVJZYAMZAYAVKVLZYAYCUROZYDMNZYTYDYBFGZYDYBVRGZYTY
      DYAYAIUDSZXTJSZJSZYBFYTUUPYCFGZYDUUQFGZYTYAXRVMSZIUDSZIJSZUUTVNUCZUUPYCFY
      TUVBEUUTVOSZUVBVOSZUVCYTUVACDZUVBCDZYTUUTCDZUVFYTUUTHDZUVHYTYQYJUVIUUDXQY
      JYQYLPYAXRVPOZUUTMNZUUTVQNUVAVSNZYTUVDCDZUVGUVECDYTUVDHDZUVMYTEHDUVIUVNVT
      UVJEUUTWAUSZUVDMNZUVLUVDUVBWBOYTUVHUVCCDUVKUUTWCNYTKUVDFGZUVBUVEFGZYTUVNU
      VQUVOUVDWDNYTUVMUVGUVQUVRWEUVPUVLUVDUVBWFOWIYTUVIUVEUVCFGUVJUUTWGNVHYTUUP
      UUOXSVMSZIJSZUVBYTUUOTDZITDZIKQZXSTDZXSKQZUUPUVTRYTYATDZUWAYTYAYQUUFXQUUI
      WHZUTZYAWJNZUWBYTWTLUWCYTWKLYTYIUWDXQYIYQYMPZXSXANYTYIUWEUWJXSWLNUUOIXSWM
      WNYTUVAUVSIJYTUWFXRTDZUVAUVSRUWHXQUWKYQXQYJUWKYLXRXANPYAXRWOOWPWQYTATDZAK
      QZUWFYCUVCRXQUWLYQXQAUUGUTPYTUUEUWMXQUUEYQUUHPAWLNUWHAYAWRWSXBYTUUPCDZYCC
      DZUUFKUUPFGZKYCFGZKYAFGZUURUUSWEYTUUPHDZUWNYTUUOHDZYGUWSYTYQYKUWTUUDUPYAI
      UMUQZXQYGYQYNPZUUOXTUROZUUPMNYTUUCUWOUUJYCMNUWGYTUWSUWPUXCUUPWDNYTUUCUWQU
      UJYCWDNYQUWRXQYAWDWHUUPYCYAXCXDWIYTUUQYAXTVMSZUUOJSZUXDYAYAVMSZJSZYBYTUWF
      UWAUUOKQZYFXTKQZUUQUXERUWHUWIYTUWTUXHUXAUUOWLNXQYFYQYPPZYTYGUXIUXBXTWLNYA
      UUOXTWMWNYTUUOUXFUXDJYTUWFUUOUXFRUWHYAXENXFYTYFUWFYAKQZUWFUXKUXGYBRUXJUWH
      YQUXKXQYAWLWHZUWHUXLXTYAYAXGWNXHXIYTUUBYRUUMUUNXKUULYSYDYBXJOXLYTUUAKYDVR
      GUUKYDXMNXN $.
      $( [15-Sep-2014] $)

    $( Any power grows slower than any exponential with base greater than
       ` 1 ` .  (Contributed by Mario Carneiro, 18-Sep-2014.) $)
    cxp2lim $p |- ( ( A e. RR /\ B e. RR /\ 1 < B ) ->
      ( n e. RR+ |-> ( ( n ^c A ) / ( B ^c n ) ) ) ~~>r 0 ) $=
      ( cr wcel c1 clt wbr crp ccxp cdiv cc0 wceq cle 1re a1i rpre syl2anc syl
      co w3a cv cmpt crli cpnf cico cres wss wa elicopnf ax-mp simplbi 0re lt01
      wb simprbi ltletrd elrp sylanbrc ssriv resmpt cif cc adantl simpl2 simpl3
      rpge0 lttrd rpcxpcl simp1 ifcl sylancl max1 sylancr rpreccl adantr divcxp
      3syl recnd syl211anc cmul rpne0 recid2 oveq2d cxpmul syl3anc rpcn 3eqtr3d
      cxp1 eqtrd mpteq2dva ovex mulcom simp2 simp3 1cxp ltleii cxplt2 syl221anc
      wne cvv mpbid eqbrtrrd cxp2limlem eqbrtrd rlimcxp rlimres2 rpdivcl sylan2
      simpr simpl1 max2 cxplea sylib lediv1 rlimsqz2 syl5eqbr eqid fmptd rpssre
      rlimresb mpbird ) ADEZBDEZFBGHZUAZCICUBZAJTZBYGJTZKTZUCZLUDHYKFUEUFTZUGZL
      UDHYFYMCYLYJUCZLUDYLIUHZYMYNMCYLIYGYLEZYGDEZLYGGHYGIEZYPYQFYGNHZFDEZYPYQY
      SUIUOOFYGUJUKZULZYPLFYGLDEZYPUMPYTYPOPUUBLFGHZYPUNPYPYQYSUUAUPZUQYGURUSZU
      TZCIYLYJVAUKYFCYLYGFANHZAFVBZJTZYIKTZYJLUUCYFUMPZYFCYLIUUKLDYOYFUUGPUULYF
      CIYGYIFUUIKTZJTZKTZUUIJTZUCCIUUKUCLUDYFCIUUPUUKYFYRUIZUUPUUJUUNUUIJTZKTZU
      UKUUQYQLYGNHZUUNIEZUUIVCEZUUPUUSMYRYQYFYGQVDZYRUUTYFYGVGVDUUQYIIEZUUMDEZU
      VAUUQBIEZYQUVDUUQYDLBGHZUVFYCYDYEYRVEZUUQLFBUUCUUQUMPYTUUQOPUVHUUDUUQUNPY
      CYDYEYRVFVHBURZUSZUVCBYGVIRZYFUVEYRYFUUIIEZUUMIEZUVEYFUUIDEZLUUIGHUVLYFYC
      YTUVNYCYDYEVJZOUUHAFDVKVLZYFLFUUIUULYTYFOPZUVPUUDYFUNPZYFYTYCFUUINHOUVOFA
      VMVNUQUUIURUSZUUIVOZUUMQVRZVPZYIUUMVIRYFUVBYRYFUUIUVPVSVPZYGUUNUUIVQVTUUQ
      UURYIUUJKUUQYIUUMUUIWATZJTZYIFJTZUURYIUUQUWDFYIJUUQUVBUUILWTZUWDFMUWCUUQU
      VLUWGYFUVLYRUVSVPUUIWBSUUIWCRWDUUQUVDUVEUVBUWEUURMUVKUWBUWCYIUUMUUIWEWFUU
      QUVDYIVCEUWFYIMUVKYIWGYIWIVRWHWDWJWKYFIUUOUUICXAUUOXAEUUQYGUUNKWLPYFCIUUO
      UCCIYGBUUMJTZYGJTZKTZUCZLUDYFCIUUOUWJUUQUUNUWIYGKUUQBYGUUMWATZJTZBUUMYGWA
      TZJTZUUNUWIUUQUWLUWNBJUUQYGVCEZUUMVCEZUWLUWNMUUQYGUVCVSZYFUWQYRYFUUMUWAVS
      ZVPZYGUUMWMRWDUUQUVFYQUWQUWMUUNMUVJUVCUWTBYGUUMWEWFUUQUVFUVEUWPUWOUWIMUVJ
      UWBUWRBUUMYGWEWFWHWDWKYFUWHDEZFUWHGHUWKLUDHYFUWHIEZUXAYFUVFUVEUXBYFYDUVGU
      VFYCYDYEWNZYFLFBUULUVQUXCUVRYCYDYEWOZVHUVIUSZUWABUUMVIRUWHQSYFFUUMJTZFUWH
      GYFUWQUXFFMUWSUUMWPSYFYEUXFUWHGHZUXDYFYTLFNHZYDLBNHZUVMYEUXGUOUVQUXHYFLFU
      MOUNWQPUXCYFUVFUXIUXEBVGSYFUVLUVMUVSUVTSFBUUMWRWSXBXCUWHCXDRXEUVSXFXCXGYP
      YFYRUUKDEZUUFUUQUUKIEZUXJUUQUUJIEZUVDUXKUUQYRUVNUXLYFYRXJZYFUVNYRUVPVPYGU
      UIVIRZUVKUUJYIXHRUUKQSXIYFYPUIZYJIEZYJDEYPYFYRUXPUUFUUQYHIEZUVDUXPUUQYRYC
      UXQUXMYCYDYEYRXKYGAVIRZUVKYHYIXHRZXIZYJQSUXOYHUUJNHZYJUUKNHZUXOYQYSYCUVNA
      UUINHZUYAYPYQYFUUBVDYPYSYFUUEVDYCYDYEYPXKZYFUVNYPUVPVPUXOYTYCUYCOUYDFAXLV
      NYGAUUIXMWSUXOYHDEZUUJDEZYIDELYIGHUIZUYAUYBUOUXOUXQUYEYPYFYRUXQUUFUXRXIYH
      QSUXOUXLUYFYPYFYRUXLUUFUXNXIUUJQSUXOUVDUYGYPYFYRUVDUUFUVKXIYIURXNYHUUJYIX
      OWFXBUXOUXPLYJNHUXTYJVGSXPXQYFIFLYKDYFCIYJVCYKUUQUXPYJVCEUXSYJWGSYKXRXSID
      UHYFXTPUULUVQYAYB $.
      $( [18-Sep-2014] $)

    $( The logarithm grows slower than any positive power.  (Contributed by
       Mario Carneiro, 15-Sep-2014.) $)
    cxploglim $p |- ( A e. RR+ ->
      ( n e. RR+ |-> ( ( log ` n ) / ( n ^c A ) ) ) ~~>r 0 ) $=
      ( vm vx crp wcel cv ce cfv co cdiv wbr cr c1 clt syl syl2anc wi wa wceq
      vz vy ccxp cmpt cc0 crli clog rpre reefcl efgt1 cxp2limlem cabs wral wrex
      cle cif wb 1re a1i adantl maxlt syl3anc simprrr reeflog ad2antrl breqtrrd
      adantr simplr simprrl rplogcl mpbird breq2 id oveq2 oveq12d fveq2d breq1d
      eflt imbi12d rcla4v mpid cmul ad2antrr relogef oveq2d cc recnd rpcn eqtrd
      mulcom wne efne0 cxpef rpne0 3eqtr4d sylibd expr sylbid ralrimdva sylancl
      com23 ifcl jctild breq1 imbi1d ralbidv rcla4ev syl6 ralimdv simpr rpcxpcl
      rexlimdva rpefcl rpdivcl wss rpssre rlim0lt relogcl rerpdivcl 3imtr4d mpd
      ) AEFZCECGZAHIZYCUCJZKJZUDUEUFLZBEBGZUGIZYHAUCJZKJZUDUEUFLZYBYDMFZNYDOLYG
      YBAMFZYMAUHZAUIPZAUJYDCUKQYBUAGZYCOLZYFULIZDGZOLZRZCEUMZUAMUNZDEUMUBGZYHO
      LZYKULIZYTOLZRZBEUMZUBMUNZDEUMYGYLYBUUDUUKDEYBUUCUUKUAMYBYQMFZSZUUCNYQHIZ
      UOLZUUNNUPZMFZUUPYHOLZUUHRZBEUMZSUUKUUMUUCUUTUUQUUMUUCUUSBEUUMYHEFZSZUURU
      UCUUHUVBUURNYHOLZUUNYHOLZSZUUCUUHRZUVBNMFZUUNMFZYHMFZUURUVEUQUVGUVBURUSUU
      MUVHUVAUULUVHYBYQUIUTZVGUVAUVIUUMYHUHZUTNUUNYHVAVBUUMUVAUVEUVFUUMUVAUVESZ
      SZUUCYIYDYIUCJZKJZULIZYTOLZUUHUVMUUCYQYIOLZUVQUVMUVRUUNYIHIZOLZUVMUUNYHUV
      SOUUMUVAUVCUVDVCUVAUVSYHTUUMUVEYHVDVEVFUVMUULYIMFZUVRUVTUQYBUULUVLVHUVMYI
      EFZUWAUVMUVIUVCUWBUVAUVIUUMUVEUVKVEUUMUVAUVCUVDVIYHVJQZYIUHPZYQYIVRQVKUVM
      UWBUUCUVRUVQRZRUWCUUBUWECYIEYCYITZYRUVRUUAUVQYCYIYQOVLUWFYSUVPYTOUWFYFUVO
      ULUWFYCYIYEUVNKUWFVMYCYIYDUCVNVOVPVQVSVTPWAUVMUVPUUGYTOUVMUVOYKULUVMUVNYJ
      YIKUVMYIYDUGIZWBJZHIZAYIWBJZHIZUVNYJUVMUWHUWJHUVMUWHYIAWBJZUWJUVMUWGAYIWB
      UVMYNUWGATYBYNUULUVLYOWCAWDPWEUVMYIWFFZAWFFZUWLUWJTUVMYIUWDWGZYBUWNUULUVL
      AWHWCZYIAWJQWIVPUVMYDWFFYDUEWKZUWMUVNUWITUVMYDYBYMUULUVLYPWCWGUVMUWNUWQUW
      PAWLPUWOYDYIWMVBUVMYHWFFZYHUEWKZUWNYJUWKTUVAUWRUUMUVEYHWHVEUVAUWSUUMUVEYH
      WNVEUWPYHAWMVBWOWEVPVQWPWQWRXAWSUUMUVHUVGUUQUVJURUUOUUNNMXBWTXCUUJUUTUBUU
      PMUUEUUPTZUUIUUSBEUWTUUFUURUUHUUEUUPYHOXDXEXFXGXHXLXIYBDUACEYFYBYCEFZSZYF
      EFZYFWFFUXBUXAYEEFZUXCYBUXAXJUXBYDEFZYCMFZUXDUXBYNUXEYBYNUXAYOVGAXMPUXAUX
      FYBYCUHUTYDYCXKQYCYEXNQYFWHPEMXOYBXPUSZXQYBDUBBEYKYBUVASZYKUXHUWAYJEFZYKM
      FUVAUWAYBYHXRUTUXHUVAYNUXIYBUVAXJYBYNUVAYOVGYHAXKQYIYJXSQWGUXGXQXTYA $.
      $( [18-Sep-2014] $)
  $}

