$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Mapping of the exponential function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d C x $.
    efgh.1 $e |- F = ( x e. CC |-> ( exp ` ( A x. x ) ) ) $.
    $( The exponential function of a scaled complex number is a group
       homomorphism from the group of complex numbers under addition to the set
       of complex numbers under multiplication.  (Contributed by Paul Chapman,
       25-Apr-2008.)  (Revised by Mario Carneiro, 11-May-2014.) $)
    efgh $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( F ` ( B + C ) ) = ( ( F ` B ) x. ( F ` C ) ) ) $=
      ( cc wcel caddc co cmul ce cfv fveq2d wceq mulcl 3adant1 oveq2 fvex fvmpt
      w3a adddi 3adant3 3adant2 efadd syl2anc eqtrd addcl syl oveqan12d 3eqtr4d
      cv ) BGHZCGHZDGHZUAZBCDIJZKJZLMZBCKJZLMZBDKJZLMZKJZUQEMZCEMZDEMZKJZUPUSUT
      VBIJZLMZVDUPURVILBCDUBNUPUTGHZVBGHZVJVDOUMUNVKUOBCPUCUMUOVLUNBDPUDUTVBUEU
      FUGUPUQGHZVEUSOUNUOVMUMCDUHQAUQBAULZKJZLMZUSGEVNUQOVOURLVNUQBKRNFURLSTUIU
      NUOVHVDOUMUNUOVFVAVGVCKACVPVAGEVNCOVOUTLVNCBKRNFUTLSTADVPVCGEVNDOVOVBLVND
      BKRNFVBLSTUJQUK $.
      $( [11-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z ph $.  $d x y z X $.
    efghgrp.1 $e |- S = { y | E. x e. X y = ( exp ` ( A x. x ) ) } $.
    efghgrp.2 $e |- G = ( x. |` ( S X. S ) ) $.
    efghgrp.3 $e |- ( ph -> A e. CC ) $.
    efghgrp.4 $e |- ( ph -> X C_ CC ) $.
    efghgrp.5 $e |- ( + |` ( X X. X ) ) e. ( SubGrpOp ` + ) $.
    $( The image of a subgroup of the group ` + ` , under the exponential
       function of a scaled complex number, is an Abelian group.  (Contributed
       by Paul Chapman, 25-Apr-2008.)  (Revised by Mario Carneiro,
       12-May-2014.) $)
    efghgrp $p |- ( ph -> G e. AbelOp ) $=
      ( cmul cc cfv caddc wceq eqid wcel a1i vz cv co ce cmpt cxp cres crn cima
      cablo wrex cab rnmpt eqtr4i df-ima wss resmpt syl rneqd cdm cgr ax-addopr
      syl5eq csubgo cnaddablo subgoablo mp2an ablogrpo resgrprn syl3anc imaeq2d
      fdmi eqtr3d xpeq12d reseq2d ax-mp grporn wa mulcl efcl sylan fmptd wfn wf
      ssid ax-mulopr ffn efgh 3expb ghsubablo eqeltrd ) AFMBNDBUBZMUCZUDOZUEZPG
      GUFUGZUHZUIZWRUFZUGZUJAFMEEUFZUGWTIAXAWSMAEWREWRAEBGWNUEZUHZWRECUBZWNQBGU
      KCULXCHBCGWNXBXBRUMUNAWOGUIZXCWRAXEWOGUGZUHXCWOGUOAXFXBAGNUPZXFXBQKBNGWNU
      QURUSVCAGWQWOAPUTNNUFZQZWPVASZXGGWQQXIAXHNPVBVLZTAWPUJSZXJXLAPUJSZWPPVDOS
      ZXLVELPWPVFVGTZWPVHURKPWPNGWPRVIVJVKVMVCZXPVNVOVCACUANWPWOPWTMWRNNWQXNALT
      PNXMPVASVEPVHVPXKVQABNWNNWOADNSZWLNSZWNNSZJXQXRVRWMNSXSDWLVSWMVTURWAWORZW
      BNNUPANWETMXHWCZAXHNMWDYAWFXHNMWGVPTAXQXDNSZUAUBZNSZVRXDYCPUCWOOXDWOOYCWO
      OMUCQZJXQYBYDYEBDXDYCWOXTWHWIWAWQRWRRWTRXOWJWK $.
      $( [12-May-2014] $) $( [25-Apr-2008] $)
  $}

  ${
    $d y z $.  $d y A $.  $d y D $.
    efif1olem1.1 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( Lemma for ~ efif1o . $)
    efif1olem1 $p |- ( ( A e. RR /\ ( x e. D /\ y e. D ) ) ->
                         ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $=
      ( cr wcel cv wa cmin co c2 clt wbr caddc wb readdcl sylancl mpbid syl3anc
      cabs cfv cpi cmul cle cioc w3a simprr syl6eleq cxr rexr adantr simpl pire
      2re remulcli elioc2 syl2anc simp1d simprl simp3d simp2d a1i ltadd1 mpbird
      lelttrd ltsubadd absdiflt mpbir2and ) CFGZAHZDGZBHZDGZIZIZVKVMJKUAUBLUCUD
      KZMNZVMVQJKVKMNZVKVMVQOKZMNZVPVSVMVKVQOKZMNZVPVMCVQOKZWBVPVMFGZCVMMNZVMWD
      UENZVPVMCWDUFKZGZWEWFWGUGZVPVMDWHVJVLVNUHEUIVPCUJGZWDFGZWIWJPVJWKVOCUKULZ
      VPVJVQFGZWLVJVOUMZLUCUOUNUPZCVQQRZCWDVMUQURSZUSZWQVPVKFGZWNWBFGVPWTCVKMNZ
      VKWDUENZVPVKWHGZWTXAXBUGZVPVKDWHVJVLVNUTEUIVPWKWLXCXDPWMWQCWDVKUQURSZUSZW
      PVKVQQRVPWEWFWGWRVAVPXAWDWBMNZVPWTXAXBXEVBVPVJWTWNXAXGPWOXFWNVPWPVCZCVKVQ
      VDTSVFVPWEWNWTVSWCPWSXHXFVMVQVKVGTVEVPVKWDVTXFWQVPWEWNVTFGWSWPVMVQQRVPWTX
      AXBXEVAVPWFWDVTMNZVPWEWFWGWRVBVPVJWEWNWFXIPWOWSXHCVMVQVDTSVFVPWTWEWNVRVSW
      AIPXFWSXHVKVMVQVHTVI $.
      $( [13-May-2014] $)

    $( Lemma for ~ efif1o . $)
    efif1olem2 $p |- ( ( A e. RR /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $=
      ( cr wcel cmul co caddc cmin cdiv cz wbr sylancl syl2anc syl3anc cc recnd
      wceq cv wa c2 cpi cmo wrex cioc clt cle 2re pire remulcli readdcl resubcl
      simpl crp 2pos pipos mulgt0ii elrpii modcl modlt wb ltadd2 mpbid ltaddsub
      a1i cc0 modge0 subge02 cxr rexr adantr elioc2 mpbir3and syl6eleqr c1 cneg
      w3a cfl cfv modval oveq2d wne gt0ne0ii redivcl mp3an23 syl remulcl subsub
      flcl zre sylancr recni simpr pnncan oveq1d 3eqtrd addcl subsub4 negsubdi2
      pncan negeqd eqtr3d 1z znegcl ax-mp zcn mulcomi mulm1i eqtri subdi eqtr4d
      syl6eqr 3eqtr2d zsubcl divcan3 eqtrd eqeltrd oveq2 eleq1d rcla4ev ) CFGZB
      UAZFGZUBZCUCUDHIZJIZCYDKIZYGUEIZKIZDGYDYKKIZYGLIZMGZYDAUAZKIZYGLIZMGZADUF
      YFYKCYHUGIZDYFYKYSGZYKFGZCYKUHNZYKYHUINZYFYHFGZYJFGZUUAYFYCYGFGZUUDYCYEUO
      ZUCUDUJUKULZCYGUMOZYFYIFGZYGUPGZUUECYDUNZYGUUHUCUDUJUKUQURUSZUTZYIYGVAOZY
      HYJUNPYFCYJJIYHUHNZUUBYFYJYGUHNZUUPYFUUJUUKUUQUULUUNYIYGVBOYFUUEUUFYCUUQU
      UPVCUUOUUFYFUUHVGUUGYJYGCVDQVEYFYCUUEUUDUUPUUBVCUUGUUOUUICYJYHVFQVEYFVHYJ
      UINZUUCYFUUJUUKUURUULUUNYIYGVIOYFUUDUUEUURUUCVCUUIUUOYHYJVJPVEYFCVKGZUUDY
      TUUAUUBUUCVSVCYCUUSYECVLVMUUICYHYKVNPVOEVPYFYMVQVRZYIYGLIZVTWAZKIZMYFYMYG
      UVCHIZYGLIZUVCYFYLUVDYGLYFYLYDYGYDJIZYGUVBHIZJIZKIZYDUVFKIZUVGKIZUVDYFYKU
      VHYDKYFYKYHYIUVGKIZKIZYHYIKIZUVGJIZUVHYFYJUVLYHKYFUUJUUKYJUVLTUULUUNYIYGW
      BOWCYFYHRGYIRGUVGRGZUVMUVOTYFYHUUISYFYIUULSYFUVGYFUUFUVBFGZUVGFGUUHYFUVBM
      GZUVQYFUVAFGZUVRYFUUJUVSUULUUJUUFYGVHWDZUVSUUHYGUUHUUMWEZYIYGWFWGWHUVAWKW
      HZUVBWLWHZYGUVBWIWMSZYHYIUVGWJQYFUVNUVFUVGJYFCRGYGRGZYDRGZUVNUVFTYFCUUGSU
      WEYFYGUUHWNZVGZYFYDYCYEWOSZCYGYDWPQWQWRWCYFUWFUVFRGZUVPUVKUVITUWIYFUWEUWF
      UWJUWGUWIYGYDWSWMZUWDYDUVFUVGWTQYFUVKYGUUTHIZUVGKIZUVDYFUVJUWLUVGKYFUVJYG
      VRZUWLYFUVFYDKIZVRZUVJUWNYFUWJUWFUWPUVJTUWKUWIUVFYDXAPYFUWOYGYFUWEUWFUWOY
      GTUWHUWIYGYDXBPXCXDUWLUUTYGHIUWNYGUUTUWGUUTMGZUUTRGZVQMGUWQXEVQXFXGZUUTXH
      XGZXIYGUWGXJXKXNWQYFUWEUWRUVBRGUVDUWMTUWHUWRYFUWTVGYFUVBUWCSYGUUTUVBXLQXM
      XOWQYFUVCRGZUVEUVCTZYFUVCMGZUXAYFUWQUVRUXCUWSUWBUUTUVBXPWMZUVCXHWHUXAUWEU
      VTUXBUWGUWAUVCYGXQWGWHXRUXDXSYRYNAYKDYOYKTZYQYMMUXEYPYLYGLYOYKYDKXTWQYAYB
      P $.
      $( [13-May-2014] $)
  $}

  ${
    $d w x y z A $.  $d w x y C $.  $d x y F $.  $d w x y z ph $.  $d y z S $.
    $d w x y z D $.
    efif1o.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    efif1o.2 $e |- C = ( `' abs " { 1 } ) $.
    ${
      efif1olem3.3 $e |- ( ph -> D C_ RR ) $.
      efif1olem3.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                           ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
      efif1olem3.5 $e |- ( ( ph /\ z e. RR ) ->
                           E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
      efif1olem3.6 $e |- S = ( sin |` ( -u ( pi / 2 ) [,] ( pi / 2 ) ) ) $.
      $( The exponential function of an imaginary number maps any interval of
         length ` 2 pi ` one-to-one onto the unit circle.  (Contributed by Paul
         Chapman, 16-Mar-2008.)  (The proof was shortened by Mario Carneiro,
         13-May-2014.) $)
      efif1olem3 $p |- ( ph -> F : D -1-1-onto-> C ) $=
        ( wceq co wcel cc syl wf1 wfo wf1o wf cv cfv wi wral ci ce wa cr sselda
        cmul cabs ccnv c1 csn cima ax-icn recn mulcl sylancr efcl absefi wfn wb
        absf ffn ax-mp ax-1cn fniniseg mp2an sylanbrc syl6eleqr cmin cc0 c2 cpi
        fmptd cdiv clt wbr wss ad2antrr simplrl recnd simplrr subcl syl2anc wne
        sseldd 2re pire remulcli recni 2pos mp3an23 cle 0re ltleii adantr abscl
        1re pm3.2i cn0 cz divcan5 subdi syl3anc fveq2d simpr oveq2 fvex 3eqtr3d
        a1i fvmpt oveq1d eqtrd 3eqtrd efeq1 eqeltrrd wrex cneg cicc simpld cexp
        mpbid sylancl simprd sq11 renegcli csin 3syl oveq1 eleq1d caddc oveq12d
        ralrimiva sqcl mulgt0ii gt0ne0ii absdiv oveq2i syl6eq mulid1i syl6breqr
        pipos absid ltdivmul mpbird eqbrtrd efsub efne0 divid nn0abscl nn0lt10b
        ine0 divcl abs00 diveq0 subeq0 ralrimivva dff13 csqr cim syl6eleq sylib
        ex sqrcl imcl absimle sqrth 2nn0 absexp sq1 syl6eqr absge0 lt01 mpanr12
        breqtrd absle syl3anbrc cres resinf1o f1oeq1 mpbir f1ocnv f1of ffvelrni
        elicc2i mp2b rehalfcl iccssre sseli remulcl rcla4v sylc npcan efadd 2cn
        rexbidv mul12 mp3an12 2z efexp ccos cre sincossq absvalsq2 fveq1i fvres
        3eqtr2d syl5eq f1ocnvfv2 eqtr3d sincl coscl pncan2 recl pncan cosq14ge0
        recoscl oveq2d efival replim 3eqtr4d mulid2 syl5ib bitr2d adantl eqeq2d
        sqrrege0 syl22anc eqeq12d 3imtr4d reximdva mpd dffo3 df-f1o ) AGFIUAZGF
        IUBZGFIUCAGFIUDZBUEZIUFZCUEZIUFZPZVUDVUFPZUGZCGUHBGUHVUAAEGUIEUEZUNQZUJ
        UFZFIAVUKGRUKVUKULRZVUMFRAGULVUKLUMVUNVUMUOUPUQURUSZFVUNVUMSRZVUMUOUFUQ
        PZVUMVUORZVUNVULSRZVUPVUNUISRZVUKSRVUSUTVUKVAUIVUKVBVCVULVDTVUKVEUOSVFZ
        UQSRZVURVUPVUQUKVGSULUOUDVVAVHSULUOVIVJZVKSUQVUMUOSVLVMVNKVOTJVTZAVUJBC
        GGAVUDGRZVUFGRZUKZUKZVUHVUIVVHVUHUKZVUDVUFVPQZVQPZVUIVVIVVJVRVSUNQZWAQZ
        VQPZVVKVVIVVMUOUFZVQPZVVNVVIVVOUQWBWCZVVPVVIVVOVVJUOUFZVVLWAQZUQWBVVIVV
        OVVRVVLUOUFZWAQZVVSVVIVVJSRZVVOVWAPZVVIVUDSRZVUFSRZVWBVVIVUDVVIGULVUDAG
        ULWDZVVGVUHLWEZAVVEVVFVUHWFZWLWGZVVIVUFVVIGULVUFVWGAVVEVVFVUHWHZWLWGZVU
        DVUFWIWJZVWBVVLSRZVVLVQWKZVWCVVLVRVSWMWNWOZWPZVVLVWOVRVSWMWNWQUUHUUAZUU
        BZVVJVVLUUCWRTVVTVVLVVRWAVVLULRZVQVVLWSWCVVTVVLPVWOVQVVLWTVWOVWQXAVVLUU
        IVMUUDUUEVVIVVSUQWBWCZVVRVVLUQUNQZWBWCZVVIVVRVVLVXAWBVVHVVRVVLWBWCVUHMX
        BVVLVWPUUFUUGVVIVVRULRZVWTVXBVGZVVIVWBVXCVWLVVJXCTVXCUQULRZVWSVQVVLWBWC
        ZUKVXDXDVWSVXFVWOVWQXEVVRUQVVLUUJWRTUUKUULVVIVVOXFRZVVQVVPVGVVIVVMXGRVX
        GVVIUIVVJUNQZUIVVLUNQZWAQZVVMXGVVIVWBVXJVVMPZVWLVWBVWMVWNUKZVUTUIVQWKZU
        KZVXKVWMVWNVWPVWRXEZVUTVXMUTUURXEZVVJVVLUIXHWRTVVIVXHUJUFZUQPZVXJXGRZVV
        IVXQUIVUDUNQZUIVUFUNQZVPQZUJUFZVXTUJUFZVYAUJUFZWAQZUQVVIVXHVYBUJVVIVUTV
        WDVWEVXHVYBPVUTVVIUTXPVWIVWKUIVUDVUFXIXJXKVVIVXTSRZVYASRZVYCVYFPVVIVUTV
        WDVYGUTVWIUIVUDVBVCVVIVUTVWEVYHUTVWKUIVUFVBZVCZVXTVYAUUMWJVVIVYFVYEVYEW
        AQZUQVVIVYDVYEVYEWAVVIVUEVUGVYDVYEVVHVUHXLVVIVVEVUEVYDPVWHEVUDVUMVYDGIV
        UKVUDPVULVXTUJVUKVUDUIUNXMXKJVXTUJXNXQTVVIVVFVUGVYEPZVWJEVUFVUMVYEGIVUK
        VUFPVULVYAUJVUKVUFUIUNXMXKJVYAUJXNXQZTXOXRVVIVYESRZVYEVQWKZVYKUQPVVIVYH
        VYNVYJVYAVDZTVVIVYHVYOVYJVYAUUNTVYEUUOWJXSXTVVIVXHSRZVXRVXSVGVVIVUTVWBV
        YQUTVWLUIVVJVBVCVXHYATYHYBVVMUUPTVVOUUQTYHVVIVVMSRZVVPVVNVGVVIVWBVYRVWL
        VWBVWMVWNVYRVWPVWRVVJVVLUUSWRTVVMUUTTYHVVIVWBVVNVVKVGZVWLVWBVWMVWNVYSVW
        PVWRVVJVVLUVAWRTYHVVIVWDVWEVVKVUIVGVWIVWKVUDVUFUVBWJYHUVIUVCBCGFIUVDVNA
        VUCVUDVUGPZCGYCZBFUHVUBVVDAWUABFAVUDFRZUKZVRVUDUVEUFZUVFUFZHUPZUFZUNQZV
        UFVPQZVVLWAQZXGRZCGYCZWUAWUCWUHULRZDUEZVUFVPQZVVLWAQZXGRZCGYCZDULUHZWUL
        WUCVRULRWUGULRZWUMWMWUCWUEUQYDZUQYEQZRZWUGVSVRWAQZYDZWVDYEQZRZWUTWUCWUE
        ULRZWVAWUEWSWCZWUEUQWSWCZWVCWUCWUDSRZWVHWUCVWDWVKWUCVWDVUDUOUFZUQPZWUCV
        UDVUORZVWDWVMUKZWUCVUDFVUOAWUBXLKUVGVVAVVBWVNWVOVGVVCVKSUQVUDUOSVLVMUVH
        ZYFZVUDUVJTZWUDUVKTZWUCWVIWVJWUCWUEUOUFZUQWSWCZWVIWVJUKZWUCWVTWUDUOUFZU
        QWSWUCWVKWVTWWCWSWCWVRWUDUVLTWUCWWCVRYGQZUQVRYGQZPZWWCUQPZWUCWWDUQWWEWU
        CWUDVRYGQZUOUFZWVLWWDUQWUCWWHVUDUOWUCVWDWWHVUDPWVQVUDUVMTZXKWUCWVKVRXFR
        WWIWWDPWVRUVNWUDVRUVOYIWUCVWDWVMWVPYJXOZUVPUVQWUCWWCULRZVQWWCWSWCZWWFWW
        GVGZWUCWVKWWLWVRWUDXCTWUCWVKWWMWVRWUDUVRTWWLWWMUKVXEVQUQWSWCWWNXDVQUQWT
        XDUVSXAWWCUQYKUVTWJYHUWAWUCWVHVXEWWAWWBVGWVSXDWUEUQUWBYIYHZYFWUCWVIWVJW
        WOYJWVAUQWUEUQXDYLXDUWKUWCZWVBWVFWUEWUFWVFWVBHUCZWVBWVFWUFUCWVBWVFWUFUD
        WWQWVFWVBYMWVFUWDZUCZUWEHWWRPWWQWWSVGOWVFWVBHWWRUWFVJUWGZWVFWVBHUWHWVBW
        VFWUFUWIUWLUWJZWVFULWUGWVEULRWVDULRZWVFULWDWVDVSULRWXBWNVSUWMVJZYLWXCWV
        EWVDUWNVMUWOYNZVRWUGUWPVCZAWUSWUBAWURDULNYSXBWURWULDWUHULWUNWUHPZWUQWUK
        CGWXFWUPWUJXGWXFWUOWUIVVLWAWUNWUHVUFVPYOXRYPUXBUWQUWRWUCWUKVYTCGWUCVVFU
        KZUIWUIUNQZUJUFZUQPZVUDVYEPZWUKVYTWXJWXIVYEUNQZUQVYEUNQZPWXGWXKWXIUQVYE
        UNYOWXGWXLVUDWXMVYEWXGWXHVYAYQQZUJUFZUIWUHUNQZUJUFZWXLVUDWXGWXNWXPUJWXG
        WXNWXPVYAVPQZVYAYQQZWXPWXGWXHWXRVYAYQWXGVUTWUHSRZVWEWXHWXRPVUTWXGUTXPWX
        GWUHWUCWUMVVFWXEXBWGZWXGVUFWXGGULVUFAVWFWUBVVFLWEWUCVVFXLWLWGZUIWUHVUFX
        IXJXRWXGWXPSRZVYHWXSWXPPWXGVUTWXTWYCUTWYAUIWUHVBVCWXGVUTVWEVYHUTWYBVYIV
        CZWXPVYAUWSWJXSXKWXGWXHSRZVYHWXOWXLPWXGVUTWUISRZWYEUTWXGWXTVWEWYFWYAWYB
        WUHVUFWIWJZUIWUIVBVCZWYDWXHVYAUWTWJWUCWXQVUDPVVFWUCWXQUIWUGUNQZUJUFZVRY
        GQZWWHVUDWUCWXQVRWYIUNQZUJUFZWYKWUCWXPWYLUJWUCWUGSRZWXPWYLPZWUCWUGWXDWG
        ZVUTVRSRWYNWYOUTUXAUIVRWUGUXCUXDTXKWUCWYISRZVRXGRWYMWYKPWUCVUTWYNWYQUTW
        YPUIWUGVBVCUXEWYIVRUXFYIXSWUCWYJWUDVRYGWUCWUGUXGUFZUIWUGYMUFZUNQZYQQZWU
        DUXHUFZUIWUEUNQZYQQZWYJWUDWUCWYRXUBWYTXUCYQWUCWYRVRYGQZXUBVRYGQZPZWYRXU
        BPZWUCWYSVRYGQZXUEYQQZXUIVPQZXUFWUEVRYGQZYQQZXULVPQZXUEXUFWUCXUJXUMXUIX
        ULVPWUCXUJUQWWDXUMWUCWYNXUJUQPWYPWUGUXITWWKWUCWVKWWDXUMPWVRWUDUXJTUXMWU
        CWYSWUEVRYGWUCWUGHUFZWYSWUEWUCXUOWUGWWRUFZWYSWUGHWWROUXKWUCWVGXUPWYSPWU
        CWVCWVGWWPWXATZWUGWVFYMUXLTUXNWUCWWQWVCXUOWUEPWWTWWPWVFWVBWUEHUXOVCUXPZ
        XRZYRWUCXUISRZXUESRZXUKXUEPWUCWYNWYSSRXUTWYPWUGUXQWYSYTYNZWUCWYNWYRSRXV
        AWYPWUGUXRWYRYTYNXUIXUEUXSWJWUCXUFSRZXULSRXUNXUFPWUCXUBSRXVCWUCXUBWUCWV
        KXUBULRZWVRWUDUXTTZWGXUBYTTWUCXUIXULSXUSXVBYBXUFXULUYAWJXOWUCWYRULRZVQW
        YRWSWCZXVDVQXUBWSWCZXUGXUHVGWUCWUTXVFWXDWUGUYCTWUCWVGXVGXUQWUGUYBTXVEWU
        CVWDXVHWVQVUDUYMTWYRXUBYKUYNYHWUCWYSWUEUIUNXURUYDYRWUCWYNWYJXUAPWYPWUGU
        YETWUCWVKWUDXUDPWVRWUDUYFTUYGXRWWJXTXBXOWXGVYNWXMVYEPWXGVYHVYNWYDVYPTVY
        EUYHTUYOUYIWXGWXJWXHVXIWAQZXGRZWUKWXGWYEWXJXVJVGWYHWXHYATWXGXVIWUJXGWXG
        WYFXVIWUJPZWYGWYFVXLVXNXVKVXOVXPWUIVVLUIXHWRTYPUYJWXGVUGVYEVUDVVFVYLWUC
        VYMUYKUYLUYPUYQUYRYSCBGFIUYSVNGFIUYTVN $.
        $( [13-May-2014] $) $( [16-Mar-2008] $)
    $}

    efif1o.3 $e |- D = ( A (,] ( A + ( 2 x. pi ) ) ) $.
    $( The exponential function of an imaginary number maps any open-below,
       closed-above interval of length ` 2 pi ` one-to-one onto the unit
       circle.  (Contributed by Paul Chapman, 16-Mar-2008.) $)
    efif1o $p |- ( A e. RR -> F : D -1-1-onto-> C ) $=
      ( vx vy vz cr wcel csin cpi c2 cdiv co cneg wbr cicc cres cmul caddc cioc
      cv clt cle w3a cxr wb rexr 2re pire remulcli readdcl mpan2 elioc2 syl2anc
      simp1 syl6bi ssrdv syl5eqss efif1olem1 efif1olem2 eqid efif1olem3 ) BLMZI
      JKACDNOPQRZSVIUARUBZEFGVHDBBPOUCRZUDRZUERZLHVHIVMLVHIUFZVMMZVNLMZBVNUGTZV
      NVLUHTZUIZVPVHBUJMVLLMZVOVSUKBULVHVKLMVTPOUMUNUOBVKUPUQBVLVNURUSVPVQVRUTV
      AVBVCIJBDHVDJKBDHVEVJVFVG $.
      $( [13-May-2014] $) $( [16-Mar-2008] $)
  $}

  ${
    $d w x y z A $.  $d x y z C $.  $d x y z D $.  $d x y F $.  $d x y z ph $.
    $d y z S $.
    efifo.1 $e |- F = ( z e. RR |-> ( exp ` ( _i x. z ) ) ) $.
    efifo.2 $e |- C = ( `' abs " { 1 } ) $.
    $( The exponential function of an imaginary number maps the reals onto the
       unit circle.  (Contributed by Mario Carneiro, 13-May-2014.) $)
    efifo $p |- F : RR -onto-> C $=
      ( cr wfo wfn crn wceq wf ci co wcel cabs c1 cc ax-mp cc0 cioc cv cmul cfv
      ce wral ccnv csn cima ax-icn recn mulcl sylancr efcl syl absefi wa wb ffn
      absf ax-1cn fniniseg mp2an sylanbrc syl6eleqr rgen fmpt mpbi wss frn cres
      cpi cmpt df-ima reseq1i clt wbr cle cxr w3a 0xr 2re pire remulcli simp1bi
      elioc2 ssriv resmpt eqtri rneqi wf1o 0re eqid caddc addid2i oveq2i eqcomi
      recni efif1o f1ofo forn mp2b 3eqtri imassrn eqsstr3i eqssi df-fo mpbir2an
      c2 ) FBCGCFHZCIZBJFBCKZXILAUAZUBMZUDUCZBNZAFUEXKXOAFXLFNZXNOUFPUGUHZBXPXN
      QNZXNOUCPJZXNXQNZXPXMQNZXRXPLQNXLQNYAUIXLUJLXLUKULXMUMUNXLUOOQHZPQNXTXRXS
      UPUQQFOKYBUSQFOURRUTQPXNOQVAVBVCEVDVEAFBXNCDVFVGZFBCURRXJBXKXJBVHYCFBCVIR
      BCSXHVKUBMZTMZUHZXJYFCYEVJZIAYEXNVLZIZBCYEVMYGYHYGAFXNVLZYEVJZYHCYJYEDVNY
      EFVHYKYHJAYEFXLYENZXPSXLVOVPZXLYDVQVPZSVRNYDFNYLXPYMYNVSUQVTXHVKWAWBWCZSY
      DXLWEVBWDWFAFYEXNWGRWHWIYEBYHWJZYEBYHGYIBJSFNYPWKASBYEYHYHWLESSYDWMMZTMYE
      YQYDSTYDYDYOWQWNWOWPWRRYEBYHWSYEBYHWTXAXBCYEXCXDXEFBCXFXG $.
      $( [13-May-2014] $)
  $}

  ${
    $d x y C $.
    circgrp.1 $e |- C = ( `' abs " { 1 } ) $.
    circgrp.2 $e |- T = ( x. |` ( C X. C ) ) $.
    $( The circle group ` T ` is an Abelian group.  (Contributed by Paul
       Chapman, 25-Mar-2008.)  (Revised by Mario Carneiro, 13-May-2014.) $)
    circgrp $p |- T e. AbelOp $=
      ( vy vx cablo wcel wtru ci cr cv cmul co ce cfv cmpt wceq cc a1i crn wrex
      cab wfo eqid efifo ax-mp rnmpt eqtr3i ax-icn ax-resscn readdsubgo efghgrp
      forn wss trud ) BGHIEFJABKEKJELMNOPZQZUAZAFLUQREKUBFUCKAURUDUSAREAURURUEZ
      CUFKAURUNUGEFKUQURUTUHUIDJSHIUJTKSUOIUKTULUMUP $.
      $( [13-May-2014] $) $( [25-Mar-2008] $)
  $}

  ${
    $d w x y z D $.  $d x y z F $.  $d w x y z ph $.  $d x y S $.
    eff1olem.1 $e |- F = ( w e. D |-> ( exp ` ( _i x. w ) ) ) $.
    eff1olem.2 $e |- S = ( `' Im " D ) $.
    eff1olem.3 $e |- ( ph -> D C_ RR ) $.
    eff1olem.4 $e |- ( ( ph /\ ( x e. D /\ y e. D ) ) ->
                       ( abs ` ( x - y ) ) < ( 2 x. pi ) ) $.
    eff1olem.5 $e |- ( ( ph /\ z e. RR ) ->
                       E. y e. D ( ( z - y ) / ( 2 x. pi ) ) e. ZZ ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in a real interval of length ` 2 x. pi ` , one-to-one
       onto the nonzero complex numbers.  (Contributed by Paul Chapman,
       16-Apr-2008.)  (The proof was shortened by Mario Carneiro,
       13-May-2014.) $)
    eff1olem $p |- ( ph -> ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) ) $=
      ( cc ce cfv cr co wceq wcel cc0 cdif cv cabs cres ccnv ci cdiv cmul caddc
      csn wss cmpt cim cima cdm cnvimass imf fdmi eqcomi 3sstr4i wf a1i feqmptd
      eff2 reseq1d resmpt eqtrd ax-mp sseli ffvelrni syl adantl crp clt wbr wne
      wa simpr eldifsn sylib simpld abscl cle absge0 simprd wb necon3bid mpbird
      abs00 ltlen sylancr mpbir2and elrp sylanbrc wf1o reeff1o f1ocnv f1of mp2b
      0re recnd ax-icn adantr c1 csin c2 cneg cicc eqid efif1olem3 3syl syl3anc
      cpi divcl absdiv absidm oveq2d divid syl2anc wfn ffn mulcl elpreima efadd
      3eqtrd fvres f1ocnvfv2 eqtr3d oveq2 fveq2d fvmpt oveq12d fveq2 syl5ibrcom
      fvex eqeq2d eqtr4d f1ocnvfv1 efcl absf ax-1cn fniniseg mp2an sseldd addcl
      ffvelrn eqeltrd syl6eleqr cbvmptv eqtri divcan2 3eqtrrd adantrl wi replim
      crim cre absef recl imcl divcan3 simprbi eleq2s 3eqtr4d syl2an id adantrr
      efne0 impbid f1o2d ) ACBGNUAUKUBZCUCZOPZBUCZUDPZOQUEZUFZPZUGUVOUVPUHRZHUF
      ZPZUIRZUJRZOGUEZGNULZUWECGUVNUMZSUNUFFUOZUNUPZGNUNFUQJUWINNQUNURUSUTVAZUW
      FUWECNUVNUMZGUEUWGUWFOUWKGUWFCNUVLONUVLOVBUWFVEVCVDVFCNGUVNVGVHVIUVMGTZUV
      NUVLTZAUWLUVMNTZUWMGNUVMUWJVJZNUVLUVMOVEVKVLVMAUVOUVLTZVRZUWDUWHGUWQUWDNT
      ZUWDUNPZFTZUWDUWHTZUWQUVSNTZUWCNTZUWRUWQUVSUWQUVPVNTZUVSQTZUWQUVPQTZUAUVP
      VOVPZUXDUWQUVONTZUXFUWQUXHUVOUAVQZUWQUWPUXHUXIVRAUWPVSUVONUAVTWAZWBZUVOWC
      VLZUWQUXGUAUVPWDVPZUVPUAVQZUWQUXHUXMUXKUVOWEVLUWQUXNUXIUWQUXHUXIUXJWFUWQU
      VPUAUVOUAUWQUXHUVPUASUVOUASWGUXKUVOWJVLWHWIZUWQUAQTUXFUXGUXMUXNVRWGXAUXLU
      AUVPWKWLWMUVPWNWOZVNQUVPUVRQVNUVQWPZVNQUVRWPVNQUVRVBWQQVNUVQWRVNQUVRWSWTV
      KVLZXBZUWQUGNTZUWBNTUXCXCUWQUWBUWQFQUWBAFQULUWPKXDUWQUDUFXEUKUOZFUWAVBZUV
      TUYATZUWBFTZAUYBUWPAFUYAHWPZUYAFUWAWPUYBABCDEUYAFXFXNXGUHRZXHUYFXIRUEZHIU
      YAXJKLMUYGXJXKZFUYAHWRUYAFUWAWSXLXDUWQUVTNTZUVTUDPZXESZUYCUWQUXHUVPNTZUXN
      UYIUXKUWQUVPUXLXBZUXOUVOUVPXOXMUWQUYJUVPUVPUDPZUHRZUVPUVPUHRZXEUWQUXHUYLU
      XNUYJUYOSUXKUYMUXOUVOUVPXPXMUWQUYNUVPUVPUHUWQUXHUYNUVPSUXKUVOXQVLXRUWQUYL
      UXNUYPXESUYMUXOUVPXSXTYFUDNYAZXENTUYCUYIUYKVRWGNQUDVBUYQUUANQUDYBVIUUBNXE
      UVTUDNUUCUUDWOZUYAFUVTUWAUUGXTZUUEZXBUGUWBYCWLZUVSUWCUUFXTUWQUWSUWBFUWQUX
      EUWBQTUWSUWBSUXRUYTUVSUWBUUQXTUYSUUHNQUNVBZUNNYAZUXAUWRUWTVRWGURNQUNYBZNU
      WDFUNYDWTWOJUUIAUWLUWPVRVRZUVMUWDSZUVOUVNSZVUEVUGVUFUVOUWDOPZSZAUWPVUIUWL
      UWQVUHUVSOPZUWCOPZUIRZUVPUVTUIRZUVOUWQUXBUXCVUHVULSUXSVUAUVSUWCYEXTUWQVUJ
      UVPVUKUVTUIUWQUVSUVQPZVUJUVPUWQUXEVUNVUJSUXRUVSQOYGVLUWQUXQUXDVUNUVPSWQUX
      PQVNUVPUVQYHWLYIUWQUWBHPZVUKUVTUWQUYDVUOVUKSUYSDUWBUGDUCZUIRZOPZVUKFHVUPU
      WBSVUQUWCOVUPUWBUGUIYJYKHEFUGEUCZUIRZOPZUMDFVURUMIEDFVVAVURVUSVUPSVUTVUQO
      VUSVUPUGUIYJYKUUJUUKUWCOYPYLVLUWQUYEUYCVUOUVTSAUYEUWPUYHXDUYRFUYAUVTHYHXT
      YIYMUWQUXHUYLUXNVUMUVOSUXKUYMUXOUVOUVPUULXMUUMUUNVUFUVNVUHUVOUVMUWDOYNYQY
      OAUWLVUGVUFUUOUWPAUWLVRZVUFVUGUVMUVNUDPZUVRPZUGUVNVVCUHRZUWAPZUIRZUJRZSVV
      BUVMUVMUURPZUGUVMUNPZUIRZUJRZVVHVVBUWNUVMVVLSUWLUWNAUWOVMZUVMUUPVLZVVBVVD
      VVIVVGVVKUJVVBVVDVVIUVQPZUVRPZVVIVVBVVCVVOUVRVVBVVCVVIOPZVVOVVBUWNVVCVVQS
      VVMUVMUUSVLZVVBVVIQTZVVOVVQSVVBUWNVVSVVMUVMUUTVLZVVIQOYGVLYRYKVVBUXQVVSVV
      PVVISWQVVTQVNVVIUVQYSWLVHVVBVVFVVJUGUIVVBVVFVVJHPZUWAPZVVJVVBVVEVWAUWAVVB
      VVQVVKOPZUIRZVVQUHRZVWCVVEVWAVVBVWCNTZVVQNTZVVQUAVQZVWEVWCSVVBVVKNTZVWFVV
      BUXTVVJNTVWIXCVVBVVJVVBUWNVVJQTVVMUVMUVAVLXBUGVVJYCWLZVVKYTVLVVBVVINTZVWG
      VVBVVIVVTXBZVVIYTVLVVBVWKVWHVWLVVIUVIVLVWCVVQUVBXMVVBUVNVWDVVCVVQUHVVBUVN
      VVLOPZVWDVVBUVMVVLOVVNYKVVBVWKVWIVWMVWDSVWLVWJVVIVVKYEXTVHVVRYMVVBVVJFTZV
      WAVWCSUWLVWNAVWNUVMUWHGUVMUWHTZUWNVWNVUBVUCVWOUWNVWNVRWGURVUDNUVMFUNYDWTU
      VCJUVDZVMEVVJVVAVWCFHVUSVVJSVUTVVKOVUSVVJUGUIYJYKIVVKOYPYLVLUVEYKAUYEVWNV
      WBVVJSUWLUYHVWPFUYAVVJHYSUVFVHXRYMYRVUGUWDVVHUVMVUGUVSVVDUWCVVGUJVUGUVPVV
      CUVRUVOUVNUDYNZYKVUGUWBVVFUGUIVUGUVTVVEUWAVUGUVOUVNUVPVVCUHVUGUVGVWQYMYKX
      RYMYQYOUVHUVJUVK $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

  ${
    $d w x y z S $.
    eff1o.1 $e |- S = ( `' Im " ( -u pi (,] pi ) ) $.
    $( The exponential function maps the set ` S ` , of complex numbers with
       imaginary part in the closed-above, open-below interval from ` -u pi `
       to ` pi ` one-to-one onto the nonzero complex numbers.  (Contributed by
       Paul Chapman, 16-Apr-2008.)  (Revised by Mario Carneiro,
       13-May-2014.) $)
    eff1o $p |- ( exp |` S ) : S -1-1-onto-> ( CC \ { 0 } ) $=
      ( vx vy vz vw cpi cneg cr wcel cc ce pire cioc co cmul caddc recni oveq2i
      cc0 csn cdif cres wf1o renegcli ci cfv cmpt eqid cxr rexr iocssre sylancl
      cv wss 2timesi addcli addcomi cmin negsubi wceq pncan mp2an eqtri 3eqtrri
      c2 efif1olem1 efif1olem2 eff1olem ax-mp ) GHZIJZAKTUAUBLAUCUDGMUEZVLCDEFV
      KGNOZAFVNUFFUNPOLUGUHZVOUIBVLVKUJJGIJVNIUOVKUKMVKGULUMCDVKVNGVKVFGPOZQOZV
      KNVQVKGGQOZQOVRVKQOZGVPVRVKQGGMRZUPSVKVRVKVMRGGVTVTUQZURVSVRGUSOZGVRGWAVT
      UTGKJZWCWBGVAVTVTGGVBVCVDVESZVGDEVKVNWDVHVIVJ $.
      $( [13-May-2014] $) $( [16-Apr-2008] $)
  $}

