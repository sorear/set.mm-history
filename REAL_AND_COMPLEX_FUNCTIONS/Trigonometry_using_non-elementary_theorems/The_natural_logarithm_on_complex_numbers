$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural logarithm on complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c log $.
  $c ^c $.

  $( Extend class notation with the natural logarithm function on complex
     numbers. $)
  clog $a class log $.

  $( Extend class notation with the complex power function. $)
  ccxp $a class ^c $.

  $( Define the natural logarithm function on complex numbers.  See
     ~ http://en.wikipedia.org/wiki/Natural_logarithm ("The natural logarithm
     function can also be defined as the inverse function of the exponential
     function"). $)
  df-log $a |- log = `' ( exp |` ( `' Im " ( -u pi (,] pi ) ) ) $.

  ${
    $d x y $.
    $( Define the power function on complex numbers.  Note that the value of
       this function when ` x = 0 ` and ` ( Re `` y ) <_ 0 , y =/= 0 ` should
       properly be undefined, but defining it by convention this way simplifies
       the domain. $)
    df-cxp $a |- ^c = ( x e. CC , y e. CC |->
      if ( x = 0 , if ( y = 0 , 1 , 0 ) , ( exp ` ( y x. ( log ` x ) ) ) ) ) $.
  $}

  $( The range of the natural logarithm function, also the principal domain of
     the exponential function.  This allows us to write the longer class
     expression as simply ` ran log ` .  (Contributed by Paul Chapman,
     21-Apr-2008.)  (Revised by Mario Carneiro, 13-May-2014.) $)
  logrn $p |- ran log = ( `' Im " ( -u pi (,] pi ) ) $=
    ( clog crn ce cim ccnv cpi cneg cioc co cima cres df-log rneqi cc0 csn cdif
    cc wf1o wfo wceq eqid eff1o f1ocnv ax-mp f1ofo forn mp2b eqtri ) ABCDEFGFHI
    JZKZEZBZUIAUKLMQNOPZUIUKRZUMUIUKSULUITUIUMUJRUNUIUIUAUBUIUMUJUCUDUMUIUKUEUM
    UIUKUFUGUH $.
    $( [13-May-2014] $) $( [21-Apr-2008] $)

  $( The natural logarithm function in terms of the exponential function
     restricted to its principal domain.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  dflog2 $p |- log = `' ( exp |` ran log ) $=
    ( clog ce cim ccnv cpi cneg cioc cima cres crn df-log reseq2i cnveqi eqtr4i
    co logrn ) ABCDEFEGOHZIZDBAJZIZDKTRSQBPLMN $.
    $( [21-Apr-2008] $)

  $( The range of the natural logarithm function includes the real numbers.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  relogrn $p |- ( A e. RR -> A e. ran log ) $=
    ( cr wcel cim ccnv cpi cneg cioc co cima clog cc cc0 clt wbr pire ax-mp 0re
    wb pipos crn cfv recn reim0 cle cxr renegcli rexr elioc2 mp2an lt0neg2 mpbi
    w3a ltleii mpbir3an syl6eqel wf wfn wa imf ffn elpreima mp2b sylanbrc logrn
    syl6eleqr ) ABCZADEFGZFHIZJZKUAVGALCZADUBZVICZAVJCZAUCVGVLMVIAUDMVICZMBCZVH
    MNOZMFUEOZVHUFCZFBCZVOVPVQVRUMSVHBCVSFPUGVHUHQPVHFMUIUJRMFNOZVQTVTWAVQSPFUK
    QULMFRPTUNUOUPLBDUQDLURVNVKVMUSSUTLBDVALAVIDVBVCVDVEVF $.
    $( [13-May-2014] $) $( [21-Apr-2008] $)

  $( The range of the natural logarithm function is a subset of the complex
     numbers.  (Contributed by Mario Carneiro, 13-May-2014.) $)
  logrncn $p |- ( A e. ran log -> A e. CC ) $=
    ( clog crn cc cim ccnv cpi cneg cioc co cima logrn cdm cnvimass cr imf fdmi
    sseqtri eqsstri sseli ) BCZDAUAEFGHGIJZKZDLUCEMDEUBNDOEPQRST $.
    $( [13-May-2014] $)

  $( The exponential function restricted to its principal domain maps
     one-to-one onto the nonzero complex numbers.  (Contributed by Paul
     Chapman, 21-Apr-2008.) $)
  eff1o2 $p |- ( exp |` ran log ) : ran log -1-1-onto-> ( CC \ { 0 } ) $=
    ( clog crn logrn eff1o ) ABCD $.
    $( [13-May-2014] $) $( [21-Apr-2008] $)

  $( The natural logarithm function maps the nonzero complex numbers one-to-one
     onto its range.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logf1o $p |- log : ( CC \ { 0 } ) -1-1-onto-> ran log $=
    ( cc cc0 csn cdif clog wf1o ce cres ccnv eff1o2 f1ocnv ax-mp wceq wb dflog2
    crn f1oeq1 mpbir ) ABCDZEPZEFZSTGTHZIZFZTSUBFUDJTSUBKLEUCMUAUDNOSTEUCQLR $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function on the positive reals in terms of the real
     exponential function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  dfrelog $p |- ( log |` RR+ ) = `' ( exp |` RR ) $=
    ( vx ce clog crn cres ccnv cr cima crp df-ima wceq relogrn ssriv ax-mp wfun
    wss cv wf1o mpbi dflog2 resabs1 rneqi wfn w3a reeff1o dff1o2 simp3i reseq2i
    3eqtri cnveqi cc0 csn logf1o f1ofun funeqi funcnvres eqtr3i reseq1 3eqtr4ri
    cc cdif ) BCDZEZFZVCGHZEZVDIEZBGEZFZCIEZVEIVDVEVCGEZDVHDZIVCGJVKVHGVBPVKVHK
    AGVBAQLMBGVBUANZUBVHGUCZVIOZVLIKZGIVHRVNVOVPUDUEGIVHUFSUGUIUHVKFZVIVFVKVHVM
    UJVDOZVQVFKCOZVRUTUKULVAZVBCRVSUMVTVBCUNNCVDTUOSGVCUPNUQCVDKVJVGKTCVDIURNUS
    $.
    $( [21-Apr-2008] $)

  $( The natural logarithm function maps the positive reals one-to-one onto the
     real numbers.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  relogf1o $p |- ( log |` RR+ ) : RR+ -1-1-onto-> RR $=
    ( vx crp cr clog cres wf1o ce crn ccnv wfun cc0 csn cdif eff1o2 wceq f1oeq1
    cc wb mp2b mpbir wfo dff1o3 simprbi ax-mp reeff1o wss relogrn ssriv resabs1
    cv f1orescnv mp2an dflog2 reseq1 ) BCDBEZFZBCGDHZEZIZBEZFZUSJZCBURCEZFZVAUQ
    QKLMZURFZVBNVFUQVEURUAVBUQVEURUBUCUDVDCBGCEZFZUECUQUFVCVGOVDVHRACUQAUJUGUHG
    CUQUICBVCVGPSTBCURUKULDUSOUOUTOUPVARUMDUSBUNBCUOUTPST $.
    $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function.  (Contributed by Paul Chapman,
     21-Apr-2008.) $)
  logcl $p |- ( ( A e. CC /\ A =/= 0 ) -> ( log ` A ) e. ran log ) $=
    ( cc wcel cc0 wne wa csn cdif clog cfv eldifsn wf1o wf logf1o f1of ffvelrni
    crn ax-mp sylbir ) ABCADEFABDGHZCAIJIQZCABDKTUAAITUAILTUAIMNTUAIORPS $.
    $( [21-Apr-2008] $)

  $( Closure of the natural logarithm function on positive reals.  (Contributed
     by Steve Rodriguez, 25-Nov-2007.) $)
  relogcl $p |- ( A e. RR+ -> ( log ` A ) e. RR ) $=
    ( crp wcel clog cres cr fvres wf1o wf relogf1o f1of ax-mp ffvelrni eqeltrrd
    cfv ) ABCADBEZOADOFABDGBFAPBFPHBFPIJBFPKLMN $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  eflog $p |- ( ( A e. CC /\ A =/= 0 ) -> ( exp ` ( log ` A ) ) = A ) $=
    ( cc wcel cc0 wne wa clog cfv ce crn cres wceq logcl syl ccnv dflog2 fveq1i
    fvres fveq2i csn cdif eldifsn wf1o eff1o2 f1ocnvfv2 sylbir syl5eq eqtr3d
    mpan ) ABCADEFZAGHZIGJZKZHZUKIHZAUJUKULCUNUOLAMUKULIRNUJUNAUMOZHZUMHZAUKUQU
    MAGUPPQSUJABDTUAZCZURALZABDUBULUSUMUCUTVAUDULUSAUMUEUIUFUGUH $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reeflog $p |- ( A e. RR+ -> ( exp ` ( log ` A ) ) = A ) $=
    ( crp wcel cc cc0 wne wa clog cfv ce wceq rpcnne0 eflog syl ) ABCADCAEFGAHI
    JIAKALAMN $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logef $p |- ( A e. ran log -> ( log ` ( exp ` A ) ) = A ) $=
    ( clog crn wcel ce cres cfv fvres fveq2d ccnv dflog2 fveq1i cc cc0 csn cdif
    wf1o wceq eff1o2 f1ocnvfv1 mpan syl5eq eqtr3d ) ABCZDZAEUDFZGZBGZAEGZBGAUEU
    GUIBAUDEHIUEUHUGUFJZGZAUGBUJKLUDMNOPZUFQUEUKARSUDULAUFTUAUBUC $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogef $p |- ( A e. RR -> ( log ` ( exp ` A ) ) = A ) $=
    ( cr wcel clog crn ce cfv wceq relogrn logef syl ) ABCADECAFGDGAHAIAJK $.
    $( [25-Nov-2007] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Paul Chapman, 21-Apr-2008.) $)
  logeftb $p |- ( ( A e. CC /\ A =/= 0 /\ B e. ran log ) ->
                  ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( cc wcel cc0 wne clog crn cfv wceq ce wb csn cdif eldifsn cres ccnv eqeq1d
    wa fvres adantr eff1o2 f1ocnvfvb mp3an1 bitr3d ancoms dflog2 fveq1i sylanbr
    wf1o eqeq1i syl6rbbr 3impa ) ACDZAEFZBGHZDZAGIZBJZBKIZAJZLZUNUOSACEMNZDZUQV
    BACEOVDUQSVAAKUPPZQZIZBJZUSUQVDVAVHLUQVDSBVEIZAJZVAVHUQVJVALVDUQVIUTABUPKTR
    UAUPVCVEUJUQVDVJVHLUBUPVCBAVEUCUDUEUFURVGBAGVFUGUHUKULUIUM $.
    $( [21-Apr-2008] $)

  $( Relationship between the natural logarithm function and the exponential
     function.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogeftb $p |- ( ( A e. RR+ /\ B e. RR ) ->
                    ( ( log ` A ) = B <-> ( exp ` B ) = A ) ) $=
    ( crp wcel cc cc0 wne wa clog crn cfv wceq ce wb cr rpcnne0 relogrn logeftb
    3expa syl2an ) ACDAEDZAFGZHBIJDZAIKBLBMKALNZBODAPBQUAUBUCUDABRST $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of ` 1 ` .  One case of Property 1a of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  log1 $p |- ( log ` 1 ) = 0 $=
    ( c1 clog cfv cc0 wceq ce ef0 crp wcel cr wb 1rp 0re relogeftb mp2an mpbir
    ) ABCDEZDFCAEZGAHIDJIQRKLMADNOP $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of ` _e ` .  One case of Property 1b of [Cohen]
     p. 301.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  loge $p |- ( log ` _e ) = 1 $=
    ( ceu clog cfv c1 wceq ce df-e eqcomi crp wcel cr ere cc0 clt wbr 1re efgt0
    wb ax-mp breqtrri elrpii relogeftb mp2an mpbir ) ABCDEZDFCZAEZAUFGHAIJDKJZU
    EUGRALMUFANUHMUFNOPDQSGTUAPADUBUCUD $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of a negative real number.  (Contributed by Mario
     Carneiro, 13-May-2014.) $)
  logneg $p |- ( A e. RR+ -> ( log ` -u A ) =
                 ( ( log ` A ) + ( _i x. pi ) ) ) $=
    ( wcel cneg clog cfv cpi cmul co wceq ce cc pire sylancl syl cc0 wb cim clt
    cr wbr crp ci caddc c1 relogcl recnd ax-icn recni mulcli efadd efipi oveq2i
    reeflog oveq1d syl5eq rpcn ax-1cn negcli mulcom mulm1 eqtrd 3eqtrd wne rpre
    crn renegcl rpne0 negeq0 necon3bid mpbid ccnv cioc cima addcl crim renegcli
    cle cxr w3a rexr ax-mp elioc2 mp2an pipos lt0neg2 mpbi lttri leidi mpbir3an
    0re syl6eqel wf wfn wa imf ffn elpreima mp2b sylanbrc logrn logeftb syl3anc
    syl6eleqr mpbird ) AUABZACZDEADEZUBFGHZUCHZIZXIJEZXFIZXEXKXGJEZXHJEZGHZAUDC
    ZGHZXFXEXGKBZXHKBZXKXOIXEXGAUEZUFZUBFUGFLUHUIZXGXHUJMXEXOXMXPGHXQXNXPXMGUKU
    LXEXMAXPGAUMUNUOXEXQXPAGHZXFXEAKBZXPKBXQYCIAUPZUDUQURAXPUSMXEYDYCXFIYEAUTNV
    AVBXEXFKBXFOVCZXIDVEZBXJXLPXEXFXEASBXFSBAVDAVFNUFXEAOVCYFAVGXEAOXFOXEYDAOIX
    FOIPYEAVHNVIVJXEXIQVKFCZFVLHZVMZYGXEXIKBZXIQEZYIBZXIYJBZXEXRXSYKYAYBXGXHVNM
    XEYLFYIXEXGSBFSBZYLFIXTLXGFVOMFYIBZYOYHFRTZFFVQTZYHVRBZYOYPYOYQYRVSPYHSBYSF
    LVPZYHVTWALYHFFWBWCLYHORTZOFRTZYQUUBUUAWDYOUUBUUAPLFWEWAWFWDYHOFYTWJLWGWCFL
    WHWIWKKSQWLQKWMYNYKYMWNPWOKSQWPKXIYIQWQWRWSWTXCXFXIXAXBXD $.
    $( [13-May-2014] $)

  $( The natural logarithm of negative ` 1 ` .  (Contributed by Paul Chapman,
     21-Apr-2008.)  (Revised by Mario Carneiro, 13-May-2014.) $)
  logm1 $p |- ( log ` -u 1 ) = ( _i x. pi ) $=
    ( c1 cneg clog cfv ci cpi cmul co caddc cc0 crp wcel wceq logneg ax-mp log1
    1rp oveq1i ax-icn pire recni mulcli addid2i 3eqtri ) ABCDZACDZEFGHZIHZJUGIH
    UGAKLUEUHMQANOUFJUGIPRUGEFSFTUAUBUCUD $.
    $( [13-May-2014] $) $( [21-Apr-2008] $)

  ${
    relogoprlem.1 $e |- ( ( ( log ` A ) e. CC /\ ( log ` B ) e. CC ) ->
                        ( exp ` ( ( log ` A ) F ( log ` B ) ) ) =
                        ( ( exp ` ( log ` A ) ) G ( exp ` ( log ` B ) ) ) ) $.
    relogoprlem.2 $e |- ( ( ( log ` A ) e. RR /\ ( log ` B ) e. RR ) ->
                          ( ( log ` A ) F ( log ` B ) ) e. RR ) $.
    $( Lemma for ~ relogmul and ~ relogdiv .  Remark of [Cohen] p. 301 ("The
       proof of Property 3 is quite similar to the proof given for Property
       2"). $)
    relogoprlem $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
        ( log ` ( A G B ) ) = ( ( log ` A ) F ( log ` B ) ) ) $=
      ( crp wcel wa clog cfv ce co reeflog fveq2d cr wceq relogcl cc recn syl
      oveqan12d syl2an relogef eqtr3d ) AGHZBGHZIZAJKZLKZBJKZLKZDMZJKZABDMZJKUI
      UKCMZUHUMUOJUFUGUJAULBDANBNUBOUFUIPHZUKPHZUNUPQUGARBRUQURIZUPLKZJKZUNUPUQ
      UISHZUKSHZVAUNQURUITUKTVBVCIUTUMJEOUCUSUPPHVAUPQFUPUDUAUEUCUE $.
      $( [25-Nov-2007] $)
  $}

  $( The natural logarithm of the product of two positive real numbers is the
     sum of natural logarithms.  Property 2 of [Cohen] p. 301, restricted to
     natural logarithms.  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogmul $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A x. B ) ) = ( ( log ` A ) + ( log ` B ) ) ) $=
    ( caddc cmul clog cfv efadd readdcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of the quotient of two positive real numbers is the
     difference of natural logarithms.  Exercise 72(a) and Property 3 of
     [Cohen] p. 301, restricted to natural logarithms.  (Contributed by Steve
     Rodriguez, 25-Nov-2007.) $)
  relogdiv $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( log ` ( A / B ) ) = ( ( log ` A ) - ( log ` B ) ) ) $=
    ( cmin cdiv clog cfv efsub resubcl relogoprlem ) ABCDAEFZBEFZGJKHI $.
    $( [25-Nov-2007] $)

  $( Exponentiation of a nonzero complex number to an integer power.
     (Contributed by Paul Chapman, 21-Apr-2008.) $)
  explog $p |- ( ( A e. CC /\ A =/= 0 /\ N e. ZZ ) ->
                  ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( cc wcel cc0 wne cz w3a clog cfv cmul co ce cexp wceq wa crn logcl logrncn
    3adant3 syl simp3 efexp syl2anc eflog oveq1d eqtr2d ) ACDZAEFZBGDZHZBAIJZKL
    MJZULMJZBNLZABNLUKULCDZUJUMUOOUHUIUPUJUHUIPULIQDUPARULSUATUHUIUJUBULBUCUDUK
    UNABNUHUIUNAOUJAUETUFUG $.
    $( [21-Apr-2008] $)

  $( Exponentiation of a positive real number to an integer power.
     (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  reexplog $p |- ( ( A e. RR+ /\ N e. ZZ ) ->
                    ( A ^ N ) = ( exp ` ( N x. ( log ` A ) ) ) ) $=
    ( crp wcel cz wa clog cfv cmul co ce cexp cc wceq relogcl recnd efexp sylan
    reeflog oveq1d adantr eqtr2d ) ACDZBEDZFBAGHZIJKHZUEKHZBLJZABLJZUCUEMDUDUFU
    HNUCUEAOPUEBQRUCUHUINUDUCUGABLASTUAUB $.
    $( [25-Nov-2007] $)

  $( The natural logarithm of positive ` A ` raised to an integer power.
     Property 4 of [Cohen] p. 301-302, restricted to natural logarithms and
     integer powers ` N ` .  (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
  relogexp $p |- ( ( A e. RR+ /\ N e. ZZ ) ->
                    ( log ` ( A ^ N ) ) = ( N x. ( log ` A ) ) ) $=
    ( crp wcel cz wa clog cfv cmul co ce cexp cc wceq relogcl recnd efexp sylan
    reeflog cr oveq1d adantr eqtrd fveq2d zre remulcl syl2anr relogef eqtr3d
    syl ) ACDZBEDZFZBAGHZIJZKHZGHZABLJZGHUOUMUPURGUMUPUNKHZBLJZURUKUNMDULUPUTNU
    KUNAOZPUNBQRUKUTURNULUKUSABLASUAUBUCUDUMUOTDZUQUONULBTDUNTDVBUKBUEVABUNUFUG
    UOUHUJUI $.
    $( [25-Nov-2007] $)

  $( The natural logarithm function on positive reals determines an isomorphism
     from the positive reals onto the reals.  (Contributed by Steve Rodriguez,
     25-Nov-2007.) $)
  relogiso $p |- ( log |` RR+ ) Isom < , < ( RR+ , RR ) $=
    ( crp cr clog cres wiso ce ccnv reefiso isocnv ax-mp wceq wb dfrelog isoeq1
    clt mpbir ) ABOOCADZEZABOOFBDZGZEZBAOOSEUAHBAOOSIJQTKRUALMABOOTQNJP $.
    $( [25-Nov-2007] $)

  ${
    $d A x y $.  $d B y $.
    $( The natural logarithm function on positive reals is strictly monotonic.
       (Contributed by Steve Rodriguez, 25-Nov-2007.) $)
    logltb $p |- ( ( A e. RR+ /\ B e. RR+ ) ->
                    ( A < B <-> ( log ` A ) < ( log ` B ) ) ) $=
      ( vx vy crp wcel wa clt wbr clog cres cv wb wral wceq fveq2 bibi12d fvres
      cfv cr wf1o wiso relogiso df-iso simpri breq1 breq1d breq2 breq2d rcla42v
      mpbi mpi breqan12d bitrd ) AEFZBEFZGZABHIZAJEKZSZBUSSZHIZAJSZBJSZHIUQCLZD
      LZHIZVEUSSZVFUSSZHIZMZDENCENZURVBMZETUSUAZVLETHHUSUBVNVLGUCCDETHHUSUDUKUE
      VKVMAVFHIZUTVIHIZMCDABEEVEAOZVGVOVJVPVEAVFHUFVQVHUTVIHVEAUSPUGQVFBOZVOURV
      PVBVFBAHUHVRVIVAUTHVFBUSPUIQUJULUOUPUTVCVAVDHAEJRBEJRUMUN $.
      $( [25-Nov-2007] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( The ` log x / x ` function is strictly decreasing on the reals greater
       than ` _e ` .  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    logdivlti $p |- ( ( ( A e. RR /\ B e. RR /\ _e <_ A ) /\ A < B )
                      -> ( ( log ` B ) / B ) < ( ( log ` A ) / A ) ) $=
      ( cr wcel ceu cle wbr clt cfv co cmul cmin c1 cc0 syl2anc syl 1re wceq cc
      wb w3a wa clog crp simpl2 simpl3 simpr wi simpl1 ere lelttr mp3an1 mp2and
      cdiv c2 2pos c3 egt2lt3 simpli 0re 2re lttri mp2an lttr mp3an12 mpani mpd
      sylanbrc ltletr rpdivcl relogcl rerpdivcl resubcl sylancl ax-mulrcl caddc
      ce reeflog ax-1cn recnd pncan3 sylancr eqtr4d mulid2 eqbrtrd a1i ltmuldiv
      elrp syl112anc mpbid difrp efgt1p eflt mpbird mulid1 df-e breqtrrd posdif
      syl5eqbrr lemul2 eqbrtrrd ltletrd relogdiv subdir syl3anc wne rpne0 div23
      efle divass eqtr3d oveq12d eqtrd 3brtr3d ltsub1 ltdivmul ) ACDZBCDZEAFGZU
      AZABHGZUBZBUCIZBUNJAUCIZAUNJZHGZYCBYEKJZHGZYBYHYCYDLJZYGYDLJZHGZYBBAUNJZU
      CIZYLMLJZYDKJZYIYJHYBYMYNYOYBYLUDDZYMCDZYBBUDDZAUDDZYPYBXRNBHGZYRXQXRXSYA
      UEZYBEBHGZYTYBXSYAUUBXQXRXSYAUFZXTYAUGZYBXQXRXSYAUBUUBUHZXQXRXSYAUIZUUAEC
      DZXQXRUUEUJEABUKULOUMYBNEHGZUUBYTNUOHGUOEHGZUUHUPUUIEUQHGURUSNUOEUTVAUJVB
      VCZYBXRUUHUUBUBYTUHZUUANCDZUUGXRUUKUTUJNEBVDVEPVFVGZBWHVHZYBXQNAHGZYSUUFY
      BXSUUOUUCYBUUHXSUUOUUJYBXQUUHXSUBUUOUHZUUFUULUUGXQUUPUTUJNEAVIVEPVFVGZAWH
      VHZBAVJOZYLVKPZYBYLCDZMCDZYNCDZYBXRYSUVAUUAUURBAVLOZQYLMVMVNZYBUVCYDCDZYO
      CDUVEYBYSUVFUURAVKPZYNYDVOOYBYMYNHGZYMVQIZYNVQIZHGZYBUVIMYNVPJZUVJHYBUVIY
      LUVLYBYPUVIYLRUUSYLVRPYBMSDZYLSDZUVLYLRVSYBYLUVDVTZMYLWAWBWCYBYNUDDZUVLUV
      JHGYBMYLHGZUVPYBMAKJZBHGZUVQYBUVRABHYBASDZUVRARYBAUUFVTZAWDPUUDWEYBUVBXRX
      QUUOUVSUVQTUVBYBQWFZUUAUUFUUQMBAWGWIWJZYBUVBUVAUVQUVPTQUVDMYLWKWBWJYNWLPW
      EYBYQUVCUVHUVKTUUTUVEYMYNWMOWNYBYNMKJZYNYOFYBYNSDUWDYNRYBYNUVEVTYNWOPYBMY
      DFGZUWDYOFGZYBUWEMVQIZYDVQIZFGZYBUWGEUWHFWPYBEAUWHFUUCYBYSUWHARUURAVRPWQW
      SYBUVBUVFUWEUWITQUVGMYDXIWBWNYBUVBUVFUVCNYNHGZUWEUWFTUWBUVGUVEYBUVQUWJUWC
      YBUVBUVAUVQUWJTQUVDMYLWRWBWJMYDYNWTWIWJXAXBYBYRYSYMYIRUUNUURBAXCOYBYOYLYD
      KJZMYDKJZLJZYJYBUVNUVMYDSDZYOUWMRUVOUVMYBVSWFYBYDUVGVTZYLMYDXDXEYBUWKYGUW
      LYDLYBBYDKJAUNJZUWKYGYBBSDZUWNUVTANXFZUWPUWKRYBBUUAVTZUWOUWAYBYSUWRUURAXG
      PZBYDAXHWIYBUWQUWNUVTUWRUWPYGRUWSUWOUWAUWTBYDAXJWIXKYBUWNUWLYDRUWOYDWDPXL
      XMXNYBYCCDZYGCDZUVFYHYKTYBYRUXAUUNBVKPZYBXRYECDZUXBUUAYBUVFYSUXDUVGUURYDA
      VLOZBYEVOOUVGYCYGYDXOXEWNYBUXAUXDXRYTYFYHTUXCUXEUUAUUMYCYEBXPWIWN $.
      $( [14-Mar-2014] $)

    $( The ` log x / x ` function is strictly decreasing on the reals greater
       than ` _e ` .  (Contributed by Mario Carneiro, 14-Mar-2014.) $)
    logdivlt $p |- ( ( ( A e. RR /\ _e <_ A ) /\ ( B e. RR /\ _e <_ B ) ) ->
                   ( A < B <-> ( ( log ` B ) / B ) < ( ( log ` A ) / A ) ) ) $=
      ( cr wcel ceu cle wbr wa clt clog cfv cdiv co wi w3a logdivlti cc0 c2 0re
      ere ex 3expa an32s adantrr wceq wo wn fveq2 id oveq12d eqcomd a1i orim12d
      ancoms con3d wb crp simpl 2pos c3 egt2lt3 simpli 2re lttri ltletr mp3an12
      mp2an imp sylanbrc relogcl rerpdivcl mpancom syl axlttri syl2anr ad2ant2r
      mpani elrp 3imtr4d impbid ) ACDZEAFGZHZBCDZEBFGZHZHZABIGZBJKZBLMZAJKZALMZ
      IGZWCWDWHWMNZWEWAWDWBWNWAWDWBWNWAWDWBOWHWMABPUAUBUCUDWGWJWLUEZWLWJIGZUFZU
      GZABUEZBAIGZUFZUGZWMWHWGXAWQWGWSWOWTWPWSWONWGWSWLWJWSWKWIABLABJUHWSUIUJUK
      ULWFWCWTWPNZWFWAXCWBWDWAWEXCWDWAWEXCWDWAWEOWTWPBAPUAUBUCUDUNUMUOWFWJCDZWL
      CDZWMWRUPWCWFBUQDZXDWFWDQBIGZXFWDWEURWDWEXGWDQEIGZWEXGQRIGREIGZXHUSXIEUTI
      GVAVBQRESVCTVDVGZQCDZECDZWDXHWEHXGNSTQEBVEVFVQVHBVRVIWICDXFXDBVJWIBVKVLVM
      WCAUQDZXEWCWAQAIGZXMWAWBURWAWBXNWAXHWBXNXJXKXLWAXHWBHXNNSTQEAVEVFVQVHAVRV
      IWKCDXMXEAVJWKAVKVLVMWJWLVNVOWAWDWHXBUPWBWEABVNVPVSVT $.
      $( [14-Mar-2014] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Value of the complex power function.  (Contributed by Mario Carneiro,
       2-Aug-2014.) $)
    cxpval $p |- ( ( A e. CC /\ B e. CC ) -> ( A ^c B ) =
      if ( A = 0 , if ( B = 0 , 1 , 0 ) , ( exp ` ( B x. ( log ` A ) ) ) ) ) $=
      ( vx vy cc cv cc0 wceq c1 cif clog cmul co ce ccxp wa simpl eqeq1d fveq2d
      cfv simpr ifbid oveq12d ifbieq12d df-cxp ax-1cn keepel elexi fvex ovmpt2a
      0cn ifex ) CDABEECFZGHZDFZGHZIGJZUOUMKTZLMZNTZJAGHZBGHZIGJZBAKTZLMZNTZJOU
      MAHZUOBHZPZUNVAUQUTVCVFVIUMAGVGVHQZRVIUPVBIGVIUOBGVGVHUAZRUBVIUSVENVIUOBU
      RVDLVKVIUMAKVJSUCSUDCDUEVAVCVFVCEVBIGEUFUKUGUHVENUIULUJ $.
      $( [2-Aug-2014] $)
  $}

  $( Value of the complex power function.  (Contributed by Mario Carneiro,
     2-Aug-2014.) $)
  cxpef $p |- ( ( A e. CC /\ A =/= 0 /\ B e. CC ) ->
      ( A ^c B ) = ( exp ` ( B x. ( log ` A ) ) ) ) $=
    ( cc wcel cc0 wne w3a ccxp co wceq c1 cif clog cmul ce cxpval 3adant2 simp2
    cfv wn df-ne sylib iffalse syl eqtrd ) ACDZAEFZBCDZGZABHIZAEJZBEJKELZBAMSNI
    OSZLZUMUFUHUJUNJUGABPQUIUKTZUNUMJUIUGUOUFUGUHRAEUAUBUKULUMUCUDUE $.
    $( [2-Aug-2014] $)

  $( Value of the complex power function when the first argument is zero.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  0cxp $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 0 ^c A ) = 0 ) $=
    ( cc wcel cc0 wne ccxp co wceq c1 cif clog cfv cmul ce 0cn cxpval mpan eqid
    iftrue ax-mp syl6eq wn df-ne iffalse sylbi sylan9eq ) ABCZADEZDAFGZADHZIDJZ
    DUGUIDDHZUKADKLMGNLZJZUKDBCUGUIUNHODAPQULUNUKHDRULUKUMSTUAUHUJUBUKDHADUCUJI
    DUDUEUF $.
    $( [2-Aug-2014] $)

  $( Relate the complex power function to the integer power function.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpexpz $p |- ( ( A e. CC /\ A =/= 0 /\ B e. ZZ ) ->
    ( A ^c B ) = ( A ^ B ) ) $=
    ( cc wcel cc0 wne cz w3a ccxp co clog cfv cmul cexp wceq zcn syl3an3 explog
    ce cxpef eqtr4d ) ACDZAEFZBGDZHABIJZBAKLMJSLZABNJUDUBUCBCDUEUFOBPABTQABRUA
    $.
    $( [2-Aug-2014] $)

  $( Relate the complex power function to the integer power function.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpexp $p |- ( ( A e. CC /\ B e. NN0 ) -> ( A ^c B ) = ( A ^ B ) ) $=
    ( cc wcel cn0 wa ccxp co cexp wceq cc0 wne c1 cif cfv 0cn ax-mp oveq2 oveq1
    iftrue wi cn wo elnn0 nncn nnne0 0cxp syl2anc 0exp eqtr4d clog cxpval mp2an
    cmul ce eqid 3eqtri exp0 eqtr4i 3eqtr4a sylbi eqeq12d syl5ibrcom adantl imp
    jaoi cz nn0z cxpexpz 3expa sylan2 an32s pm2.61dane ) ACDZBEDZFZABGHZABIHZJZ
    AKVPAKJZVSVOVTVSUAVNVOVSVTKBGHZKBIHZJZVOBUBDZBKJZUCWCBUDWDWCWEWDWAKWBWDBCDB
    KLWAKJBUEBUFBUGUHBUIUJWEKKGHZKKIHZWAWBWFMWGWFKKJZWHMKNZKKUKOUNHUOOZNZWIMKCD
    ZWLWFWKJPPKKULUMWHWKWIJKUPZWHWIWJTQWHWIMJWMWHMKTQUQWLWGMJPKURQUSBKKGRBKKIRU
    TVFVAVTVQWAVRWBAKBGSAKBISVBVCVDVEVNAKLZVOVSVOVNWNFBVGDZVSBVHVNWNWOVSABVIVJV
    KVLVM $.
    $( [2-Aug-2014] $)

  $( Logarithm of a complex power.  (Contributed by Mario Carneiro,
     2-Aug-2014.) $)
  logcxp $p |- ( ( A e. RR+ /\ B e. RR ) ->
    ( log ` ( A ^c B ) ) = ( B x. ( log ` A ) ) ) $=
    ( crp wcel cr wa ccxp co clog cfv cmul ce cc cc0 wne wceq rpcn adantr rpne0
    simpr recnd cxpef syl3anc fveq2d relogcl remulcl syl2anr relogef syl eqtrd
    id ) ACDZBEDZFZABGHZIJBAIJZKHZLJZIJZUQUNUOURIUNAMDZANOZBMDUOURPULUTUMAQRULV
    AUMASRUNBULUMTUAABUBUCUDUNUQEDZUSUQPUMUMUPEDVBULUMUKAUEBUPUFUGUQUHUIUJ $.
    $( [2-Aug-2014] $)

  $( Value of the complex power function when the second argument is zero.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxp0 $p |- ( A e. CC -> ( A ^c 0 ) = 1 ) $=
    ( cc wcel cc0 ccxp co cexp c1 cn0 wceq 0nn0 cxpexp mpan2 exp0 eqtrd ) ABCZA
    DEFZADGFZHPDICQRJKADLMANO $.
    $( [2-Aug-2014] $)

  $( Value of the complex power function at one.  (Contributed by Mario
     Carneiro, 2-Aug-2014.) $)
  cxp1 $p |- ( A e. CC -> ( A ^c 1 ) = A ) $=
    ( cc wcel c1 ccxp co cexp cn0 wceq 1nn0 cxpexp mpan2 exp1 eqtrd ) ABCZADEFZ
    ADGFZAODHCPQIJADKLAMN $.
    $( [2-Aug-2014] $)

  $( Value of the complex power function at one.  (Contributed by Mario
     Carneiro, 2-Aug-2014.) $)
  1cxp $p |- ( A e. CC -> ( 1 ^c A ) = 1 ) $=
    ( cc wcel c1 ccxp co clog cfv cmul ce cc0 wceq ax-1cn ax-1ne0 cxpef mp3an12
    wne log1 oveq2i mul01 syl5eq fveq2d ef0 syl6eq eqtrd ) ABCZDAEFZADGHZIFZJHZ
    DDBCDKQUFUGUJLMNDAOPUFUJKJHDUFUIKJUFUIAKIFKUHKAIRSATUAUBUCUDUE $.
    $( [2-Aug-2014] $)

  $( Write the exponential function as an exponent to the power ` _e ` .
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  ecxp $p |- ( A e. CC -> ( _e ^c A ) = ( exp ` A ) ) $=
    ( cc wcel ceu ccxp co clog cfv cmul ce cc0 wne wceq ere recni epos gt0ne0ii
    cxpef mp3an12 c1 loge oveq2i mulid1 syl5eq fveq2d eqtrd ) ABCZDAEFZADGHZIFZ
    JHZAJHDBCDKLUGUHUKMDNODNPQDARSUGUJAJUGUJATIFAUITAIUAUBAUCUDUEUF $.
    $( [2-Aug-2014] $)

  $( Closure of the complex power function.  (Contributed by Mario Carneiro,
     2-Aug-2014.) $)
  cxpcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A ^c B ) e. CC ) $=
    ( cc wcel wa ccxp co cc0 wceq c1 cif clog cfv cmul cxpval ax-1cn 0cn keepel
    ce syl a1i wn wne df-ne crn logcl logrncn mulcl syl2anr an32s efcl sylan2br
    id ifclda eqeltrd ) ACDZBCDZEZABFGAHIZBHIZJHKZBALMZNGZSMZKCABOURUSVAVDCVACD
    URUSEUTJHCPQRUAUSUBURAHUCZVDCDZAHUDURVEEVCCDZVFUPVEUQVGUQUQVBCDZVGUPVEEZUQU
    MVIVBLUEDVHAUFVBUGTBVBUHUIUJVCUKTULUNUO $.
    $( [2-Aug-2014] $)

  $( Real closure of the complex power function.  (Contributed by Mario
     Carneiro, 2-Aug-2014.) $)
  recxpcl $p |- ( ( A e. RR /\ 0 <_ A /\ B e. RR ) -> ( A ^c B ) e. RR ) $=
    ( cr wcel cc0 cle wbr w3a ccxp co wceq c1 cif clog cfv cc recn wa 0re syl
    cmul ce cxpval syl2an 3adant2 1re keepel a1i wn wne df-ne simpl3 crp simpl1
    clt wb wi idi ltlen sylancr simpl2 simpr mpbir2and sylanbrc relogcl remulcl
    elrp syl2anc reefcl sylan2br ifclda eqeltrd ) ACDZEAFGZBCDZHZABIJZAEKZBEKZL
    EMZBANOZUAJZUBOZMZCVMVOVQWDKZVNVMAPDBPDWEVOAQBQABUCUDUEVPVRVTWCCVTCDVPVRRVS
    LECUFSUGUHVRUIVPAEUJZWCCDZAEUKVPWFRZWBCDZWGWHVOWACDZWIVMVNVOWFULWHAUMDZWJWH
    VMEAUOGZWKVMVNVOWFUNZWHWLVNWFWHECDVMWLVNWFRUPSWHVMUQWMUREAUSUTVMVNVOWFVAVPW
    FVBVCAVGVDAVETBWAVFVHWBVITVJVKVL $.
    $( [2-Aug-2014] $)

  $( Positive real closure of the complex power function.  (Contributed by
     Mario Carneiro, 2-Aug-2014.) $)
  rpcxpcl $p |- ( ( A e. RR+ /\ B e. RR ) -> ( A ^c B ) e. RR+ ) $=
    ( crp wcel cr wa ccxp cc0 clt wbr cle rprege0 recxpcl 3expa sylan clog cmul
    co cfv cc ce relogcl remulcl syl2anr efgt0 syl wne wceq rpcnne0 recn syl2an
    id cxpef breqtrrd elrp sylanbrc ) ACDZBEDZFZABGRZEDZHUTIJUTCDUQAEDZHAKJZFUR
    VAALVBVCURVAABMNOUSHBAPSZQRZUASZUTIUSVEEDZHVFIJURURVDEDVGUQURULAUBBVDUCUDVE
    UEUFUQATDZAHUGZFBTDZUTVFUHZURAUIBUJVHVIVJVKABUMNUKUNUTUOUP $.
    $( [2-Aug-2014] $)

  $( Complex exponentiation is nonzero if its mantissa is nonzero.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpne0 $p |- ( ( A e. CC /\ A =/= 0 /\ B e. CC ) -> ( A ^c B ) =/= 0 ) $=
    ( cc wcel cc0 wne w3a ccxp co clog cfv ce cxpef wa id crn logcl logrncn syl
    cmul mulcl syl2anr 3impa efne0 eqnetrd ) ACDZAEFZBCDZGZABHIBAJKZTIZLKZEABMU
    IUKCDZULEFUFUGUHUMUHUHUJCDZUMUFUGNZUHOUOUJJPDUNAQUJRSBUJUAUBUCUKUDSUE $.
    $( [2-Aug-2014] $)

  $( Sum of exponents law for complex exponentiation.  Proposition 10-4.2(a) of
     [Gleason] p. 135.  (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpadd $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ B e. CC /\ C e. CC ) ->
    ( A ^c ( B + C ) ) = ( ( A ^c B ) x. ( A ^c C ) ) ) $=
    ( cc wcel cc0 wne wa caddc co clog cfv cmul ccxp wceq syl3anc mulcl syl2anc
    ce cxpef w3a simp2 simp3 simp1 logcl logrncn 3syl adddir fveq2d efadd eqtrd
    crn simp1l simp1r addcl 3adant1 oveq12d 3eqtr4d ) ADEZAFGZHZBDEZCDEZUAZBCIJ
    ZAKLZMJZSLZBVFMJZSLZCVFMJZSLZMJZAVENJZABNJZACNJZMJVDVHVIVKIJZSLZVMVDVGVQSVD
    VBVCVFDEZVGVQOVAVBVCUBZVAVBVCUCZVDVAVFKULEVSVAVBVCUDAUEVFUFUGZBCVFUHPUIVDVI
    DEZVKDEZVRVMOVDVBVSWCVTWBBVFQRVDVCVSWDWAWBCVFQRVIVKUJRUKVDUSUTVEDEZVNVHOUSU
    TVBVCUMZUSUTVBVCUNZVBVCWEVABCUOUPAVETPVDVOVJVPVLMVDUSUTVBVOVJOWFWGVTABTPVDU
    SUTVCVPVLOWFWGWAACTPUQUR $.
    $( [2-Aug-2014] $)

  $( Value of a nonzero complex number raised to a complex power plus one.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpp1 $p |- ( ( A e. CC /\ A =/= 0 /\ B e. CC ) ->
    ( A ^c ( B + 1 ) ) = ( ( A ^c B ) x. A ) ) $=
    ( cc wcel cc0 wne w3a c1 caddc co ccxp cmul wceq ax-1cn cxpadd mp3an3 3impa
    wa cxp1 3ad2ant1 oveq2d eqtrd ) ACDZAEFZBCDZGZABHIJKJZABKJZAHKJZLJZUHALJUCU
    DUEUGUJMZUCUDRUEHCDUKNABHOPQUFUIAUHLUCUDUIAMUEASTUAUB $.
    $( [2-Aug-2014] $)

  $( Value of a complex number raised to a negative power.  (Contributed by
     Mario Carneiro, 2-Aug-2014.) $)
  cxpneg $p |- ( ( A e. CC /\ A =/= 0 /\ B e. CC ) ->
    ( A ^c -u B ) = ( 1 / ( A ^c B ) ) ) $=
    ( cc wcel cc0 wne w3a ccxp co cneg cmul cdiv c1 simp1 simp3 negcl syl cxpcl
    wceq syl2anc cxpne0 divcan3 syl3anc caddc negid oveq2d simp2 syl211anc cxp0
    cxpadd 3eqtr3d oveq1d eqtr3d ) ACDZAEFZBCDZGZABHIZABJZHIZKIZURLIZUTMURLIUQU
    TCDZURCDZUREFVBUTSUQUNUSCDZVCUNUOUPNZUQUPVEUNUOUPOZBPQZAUSRTUQUNUPVDVFVGABR
    TABUAUTURUBUCUQVAMURLUQABUSUDIZHIZAEHIZVAMUQVIEAHUQUPVIESVGBUEQUFUQUNUOUPVE
    VJVASVFUNUOUPUGVGVHABUSUJUHUQUNVKMSVFAUIQUKULUM $.
    $( [2-Aug-2014] $)

  $( Nonnegative exponentiation with a real exponent is nonnegative.
     (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  cxpge0 $p |- ( ( A e. RR /\ 0 <_ A /\ B e. RR ) -> 0 <_ ( A ^c B ) ) $=
    ( cr wcel cc0 cle wbr ccxp co wa clt wceq wo wb 0re leloe crp c1 syl5breqr
    cc mpan adantr wi elrp rpcxpcl rpge0 syl ex sylbir impancom 1re lt01 ltleii
    0cn cxp0 ax-mp breqtrri simpr oveq2d wne leidi recn sylan pm2.61dane adantl
    0cxp oveq1 breq2d syl5ibcom jaod sylbid 3impia 3com23 ) ACDZBCDZEAFGZEABHIZ
    FGZVNVOVPVRVNVOJZVPEAKGZEALZMZVRVNVPWBNZVOECDVNWCOEAPUAUBVSVTVRWAVNVTVOVRVN
    VTJAQDZVOVRUCAUDWDVOVRWDVOJVQQDVRABUEVQUFUGUHUIUJVSEEBHIZFGZWAVRVOWFVNVOWFB
    EVOBELZJZEEEHIZWEFERWIFEROUKULUMETDWIRLUNEUOUPUQWHBEEHVOWGURUSSVOBEUTZJEEWE
    FEOVAVOBTDWJWEELBVBBVFVCSVDVEWAWEVQEFEABHVGVHVIVJVKVLVM $.
    $( [2-Aug-2014] $)

  ${
    mulcxp.1 $e |- ( ph -> A e. CC ) $.
    mulcxp.2 $e |- ( ph -> C e. CC ) $.
    $( Lemma for ~ mulcxp . $)
    mulcxplem $p |- ( ph -> ( 0 ^c C ) = ( ( A ^c C ) x. ( 0 ^c C ) ) ) $=
      ( cc0 ccxp co cmul wceq c1 cc wcel cxp0 syl oveq1d ax-1cn mulid1i syl6req
      oveq2 0cn ax-mp syl6eq oveq12d eqeq12d syl5ibrcom wa cxpcl syl2anc adantr
      wne mul01 0cxp sylan oveq2d 3eqtr4rd ex pm2.61dne ) AFCGHZBCGHZUSIHZJZCFA
      VBCFJZKBFGHZKIHZJAVEKKIHKAVDKKIABLMZVDKJDBNOPKQRSVCUSKVAVEVCUSFFGHZKCFFGT
      FLMVGKJUAFNUBUCZVCUTVDUSKICFBGTVHUDUEUFACFUKZVBAVIUGZUTFIHZFVAUSVJUTLMZVK
      FJAVLVIAVFCLMZVLDEBCUHUIUJUTULOVJUSFUTIAVMVIUSFJECUMUNZUOVNUPUQUR $.
      $( [2-Aug-2014] $)
  $}

  $( Complex exponentiation of a product.  Proposition 10-4.2(c) of [Gleason]
     p. 135.  (Contributed by Mario Carneiro, 2-Aug-2014.) $)
  mulcxp $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) /\
    C e. CC ) -> ( ( A x. B ) ^c C ) = ( ( A ^c C ) x. ( B ^c C ) ) ) $=
    ( wcel cc0 wbr wa cmul ccxp wceq syl oveq1d syl2anc clog cfv adantr syl3anc
    cc co ce cr cle simp1l recnd mul01 simp3 mulcxplem eqtrd oveq2 oveq1 oveq2d
    w3a eqeq12d syl5ibrcom wne wi simp2l mul02 cxpcl sylancr mulcom 3eqtrd a1dd
    0cn caddc crp clt wb 0re ltlen simpl1r simprl mpbir2and elrp simpl2r simprr
    sylanbrc relogmul crn logcl logrncn adddi fveq2d mulcl efadd syl22anc cxpef
    mulne0 oveq12d 3eqtr4d exp32 pm2.61dne ) AUADZEAUBFZGZBUADZEBUBFZGZCRDZULZA
    BHSZCISZACISZBCISZHSZJZBEWTXFBEJZAEHSZCISZXCECISZHSZJWTXIXJXKWTXHECIWTARDZX
    HEJWTAWMWNWRWSUCZUDZAUEKLWTACXNWOWRWSUFZUGUHXGXBXIXEXKXGXAXHCIBEAHUILXGXDXJ
    XCHBECIUJUKUMUNWTBEUOZXFUPAEWTAEJZXFXPWTXFXQEBHSZCISZXJXDHSZJWTXSXJXDXJHSZX
    TWTXRECIWTBRDZXREJWTBWOWPWQWSUQZUDZBURKLWTBCYDXOUGWTXDRDZXJRDZYAXTJWTYBWSYE
    YDXOBCUSMWTERDWSYFVDXOECUSUTXDXJVAMVBXQXBXSXEXTXQXAXRCIAEBHUJLXQXCXJXDHAECI
    UJLUMUNVCWTAEUOZXPXFWTYGXPGZGZCXANOZHSZTOZCANOZHSZTOZCBNOZHSZTOZHSZXBXEYIYL
    YNYQVESZTOZYSYIYKYTTYIYKCYMYPVESZHSZYTYIYJUUBCHYIAVFDZBVFDZYJUUBJYIWMEAVGFZ
    UUDWTWMYHXMPZYIUUFWNYGYIEUADZWMUUFWNYGGVHVIUUGEAVJUTWMWNWRWSYHVKWTYGXPVLZVM
    AVNVQYIWPEBVGFZUUEWTWPYHYCPZYIUUJWQXPYIUUHWPUUJWQXPGVHVIUUKEBVJUTWPWQWOWSYH
    VOWTYGXPVPZVMBVNVQABVRMUKYIWSYMRDZYPRDZUUCYTJWTWSYHXOPZYIYMNVSZDZUUMYIXLYGU
    UQWTXLYHXNPZUUIAVTMYMWAKZYIYPUUPDZUUNYIYBXPUUTWTYBYHYDPZUULBVTMYPWAKZCYMYPW
    BQUHWCYIYNRDZYQRDZUUAYSJYIWSUUMUVCUUOUUSCYMWDMYIWSUUNUVDUUOUVBCYPWDMYNYQWEM
    UHYIXARDZXAEUOZWSXBYLJYIXLYBUVEUURUVAABWDMYIXLYGYBXPUVFUURUUIUVAUULABWHWFUU
    OXACWGQYIXCYOXDYRHYIXLYGWSXCYOJUURUUIUUOACWGQYIYBXPWSXDYRJUVAUULUUOBCWGQWIW
    JWKWLWL $.
    $( [2-Aug-2014] $)

  $( Complex exponentiation of a reciprocal. $)
  cxprec $p |- ( ( A e. RR+ /\ B e. CC ) ->
                 ( ( 1 / A ) ^c B ) = ( 1 / ( A ^c B ) ) ) $=
    ( crp wcel cc wa ccxp co c1 cdiv cmul cc0 wne wceq syl cxpcl adantr syl2anc
    rpcn syl3anc rpreccl sylan simpr rpne0 cxpne0 divcan3 oveq1d cr cle rprege0
    recid wbr mulcxp 1cxp 3eqtr3d eqtr3d ) ACDZBEDZFZABGHZIAJHZBGHZKHZUTJHZVBIU
    TJHUSVBEDZUTEDZUTLMZVDVBNUQVAEDZURVEUQVACDZVHAUAZVASOVABPUBUSAEDZURVFUQVKUR
    ASQZUQURUCZABPRUSVKALMZURVGVLUQVNURAUDQZVMABUETVBUTUFTUSVCIUTJUSAVAKHZBGHZI
    BGHZVCIUSVPIBGUSVKVNVPINVLVOAUKRUGUSAUHDLAUIULFZVAUHDLVAUIULFZURVQVCNUQVSUR
    AUJQUQVTURUQVIVTVJVAUJOQVMAVABUMTUSURVRINVMBUNOUOUGUP $.
    $( [2-Aug-2014] $)

  $( Product of exponents law for complex exponentiation.  Proposition
     10-4.2(b) of [Gleason] p. 135.  (Contributed by Mario Carneiro,
     2-Aug-2014.) $)
  cxpmul $p |- ( ( A e. RR+ /\ B e. RR /\ C e. CC ) ->
                 ( A ^c ( B x. C ) ) = ( ( A ^c B ) ^c C ) ) $=
    ( wcel cr cc cmul co clog cfv ccxp wceq recnd syl syl3anc syl2anc cc0 cxpef
    ce wne crp simp3 simp2 simp1 relogcl mulass mulcom oveq1d rpcn rpne0 fveq2d
    w3a remulcl relogef eqtrd oveq2d 3eqtr4d mulcl cxpcl cxpne0 ) AUADZBEDZCFDZ
    ULZBCGHZAIJZGHZSJZCABKHZIJZGHZSJZAVEKHZVICKHZVDVGVKSVDCBGHZVFGHZCBVFGHZGHZV
    GVKVDVCBFDZVFFDVPVRLVAVBVCUBZVDBVAVBVCUCZMZVDVFVDVAVFEDZVAVBVCUDZAUENZMCBVF
    UFOVDVEVOVFGVDVSVCVEVOLWBVTBCUGPUHVDVJVQCGVDVJVQSJZIJZVQVDVIWFIVDAFDZAQTZVS
    VIWFLVDVAWHWDAUINZVDVAWIWDAUJNZWBABROUKVDVQEDZWGVQLVDVBWCWLWAWEBVFUMPVQUNNU
    OUPUQUKVDWHWIVEFDZVMVHLWJWKVDVSVCWMWBVTBCURPAVEROVDVIFDZVIQTZVCVNVLLVDWHVSW
    NWJWBABUSPVDWHWIVSWOWJWKWBABUTOVTVICROUQ $.
    $( [2-Aug-2014] $)

  ${
    $d k x A $.  $d k x B $.  $d x C $.
    $( Product of exponents law for complex exponentiation.  Variation on
       ~ cxpmul with more general conditions on ` A ` and ` B ` when ` C ` is
       an integer.  (Contributed by Mario Carneiro, 9-Aug-2014.) $)
    cxpmul2 $p |- ( ( A e. CC /\ B e. CC /\ C e. NN0 ) ->
                   ( A ^c ( B x. C ) ) = ( ( A ^c B ) ^ C ) ) $=
      ( cc wcel cmul co ccxp cexp wceq wa wi cc0 c1 oveq2d eqeq12d syl ad2antrr
      oveq2 eqtrd vx vk cn0 cv caddc imbi2d cxp0 adantr mul01 adantl cxpcl exp0
      3eqtr4d oveq1 0cn ax-mp ax-1cn mulid1i eqtr4i simplr simpr oveq1d nn0p1nn
      cn nncn mul02 oveq12d nn0cn syl6eq 3eqtr4a wne simpll mulcl syl2anc nnne0
      mulne0 syl22anc 3eqtr4rd pm2.61dane adddi syl3anc mulid1 cxpadd syl211anc
      0cxp a1i expp1 sylan syl5ibr expcom a2d nn0ind com12 3impia ) ADEZBDEZCUC
      EZABCFGZHGZABHGZCIGZJZWQWOWPKZXBXCABUAUDZFGZHGZWTXDIGZJZLXCABMFGZHGZWTMIG
      ZJZLXCABUBUDZFGZHGZWTXMIGZJZLXCABXMNUEGZFGZHGZWTXRIGZJZLXCXBLUAUBCXDMJZXH
      XLXCYCXFXJXGXKYCXEXIAHXDMBFSOXDMWTISPUFXDXMJZXHXQXCYDXFXOXGXPYDXEXNAHXDXM
      BFSOXDXMWTISPUFXDXRJZXHYBXCYEXFXTXGYAYEXEXSAHXDXRBFSOXDXRWTISPUFXDCJZXHXB
      XCYFXFWSXGXAYFXEWRAHXDCBFSOXDCWTISPUFXCAMHGZNXJXKWOYGNJWPAUGUHXCXIMAHWPXI
      MJWOBUIUJOXCWTDEZXKNJABUKZWTULQUMXMUCEZXCXQYBXCYJXQYBLXQYBXCYJKZXOWTFGZXP
      WTFGZJXOXPWTFUNYKXTYLYAYMYKXTYLJZAMYKAMJZKZYNBMYPBMJZKZMMHGZNNFGZXTYLYSNY
      TMDEYSNJUOMUGUPZNUQURUSYRAMXSMHYKYOYQUTZYRXSMXRFGZMYRBMXRFYPYQVAZVBYRXRDE
      ZUUCMJYKUUEYOYQYKXRVDEZUUEYJUUFXCXMVCUJZXRVEQZRXRVFQTVGYRXONWTNFYRXOYSNYR
      AMXNMHUUBYRXNMXMFGZMYRBMXMFUUDVBYRXMDEZUUIMJYKUUJYOYQYJUUJXCXMVHUJZRXMVFQ
      TVGUUAVIYRWTYSNYRAMBMHUUBUUDVGUUAVIVGVJYPBMVKZKZXOMFGZMYLXTUUMXODEZUUNMJU
      UMWOXNDEZUUOYKWOYOUULWOWPYJVLZRYKUUPYOUULYKWPUUJUUPWOWPYJUTZUUKBXMVMVNZRA
      XNUKVNXOUIQUUMWTMXOFUUMWTMBHGZMUUMAMBHYKYOUULUTZVBUUMWPUULUUTMJYKWPYOUULU
      URRZYPUULVAZBWEVNTOUUMXTMXSHGZMUUMAMXSHUVAVBUUMXSDEZXSMVKZUVDMJUUMWPUUEUV
      EUVBYKUUEYOUULUUHRZBXRVMVNUUMWPUULUUEXRMVKZUVFUVBUVCUVGYKUVHYOUULYKUUFUVH
      UUGXRVOQRBXRVPVQXSWEVNTVRVSYKAMVKZKZXTAXNBUEGZHGZYLUVJXSUVKAHUVJXSXNBNFGZ
      UEGZUVKUVJWPUUJNDEZXSUVNJYKWPUVIUURUHZYKUUJUVIUUKUHUVOUVJUQWFBXMNVTWAUVJU
      VMBXNUEUVJWPUVMBJUVPBWBQOTOUVJWOUVIUUPWPUVLYLJYKWOUVIUUQUHYKUVIVAYKUUPUVI
      UUSUHUVPAXNBWCWDTVSXCYHYJYAYMJYIWTXMWGWHPWIWJWKWLWMWN $.
      $( [9-Aug-2014] $)
  $}

  $( Ordering property for complex exponentiation.  (Contributed by Mario
     Carneiro, 2-Aug-2014.) $)
  cxplt $p |- ( ( ( A e. RR /\ 1 < A ) /\ ( B e. RR /\ C e. RR ) ) ->
                ( B < C <-> ( A ^c B ) < ( A ^c C ) ) ) $=
    ( cr wcel c1 clt wbr wa cfv cmul co ce wb cc0 a1i syl2anc wceq cc recnd crp
    clog ccxp simprl simpll 0re 1re lt01 simplr lttrd elrp sylanbrc relogcl syl
    remulcl simprr ef0 reeflog 3brtr4d mpbird ltmul1 syl112anc wne gt0ne0 cxpef
    eflt syl3anc breq12d 3bitr4d ) ADEZFAGHZIZBDEZCDEZIZIZBAUBJZKLZCVQKLZGHZVRM
    JZVSMJZGHZBCGHZABUCLZACUCLZGHVPVRDEZVSDEZVTWCNVPVMVQDEZWGVLVMVNUDZVPAUAEZWI
    VPVJOAGHZWKVJVKVOUEZVPOFAODEZVPUFPZFDEVPUGPWMOFGHVPUHPVJVKVOUIZUJZAUKULZAUM
    UNZBVQUOQVPVNWIWHVLVMVNUPZWSCVQUOQVRVSVFQVPVMVNWIOVQGHZWDVTNWJWTWSVPXAOMJZV
    QMJZGHZVPFAXBXCGWPXBFRVPUQPVPWKXCARWRAURUNUSVPWNWIXAXDNWOWSOVQVFQUTBCVQVAVB
    VPWEWAWFWBGVPASEZAOVCZBSEWEWARVPAWMTZVPVJWLXFWMWQAVDQZVPBWJTABVEVGVPXEXFCSE
    WFWBRXGXHVPCWTTACVEVGVHVI $.
    $( [2-Aug-2014] $)

  $( Ordering property for complex exponentiation.  (Contributed by Mario
     Carneiro, 2-Aug-2014.) $)
  cxple $p |- ( ( ( A e. RR /\ 1 < A ) /\ ( B e. RR /\ C e. RR ) ) ->
                ( B <_ C <-> ( A ^c B ) <_ ( A ^c C ) ) ) $=
    ( cr wcel c1 clt wbr wa wn ccxp co cle wb lenlt cc0 0re a1i recxpcl syl3anc
    cxplt ancom2s notbid adantl simpll 1re lt01 simplr wi ltle mpan sylc simprl
    lttrd simprr syl2anc 3bitr4d ) ADEZFAGHZIZBDEZCDEZIZIZCBGHZJZACKLZABKLZGHZJ
    ZBCMHZVHVGMHZVDVEVIUTVBVAVEVINACBUAUBUCVCVKVFNUTBCOUDVDVHDEZVGDEZVLVJNVDURP
    AMHZVAVMURUSVCUEZVDURPAGHZVOVPVDPFAPDEZVDQRFDEVDUFRVPPFGHVDUGRURUSVCUHUNVRU
    RVQVOUIQPAUJUKULZUTVAVBUMABSTVDURVOVBVNVPVSUTVAVBUOACSTVHVGOUPUQ $.
    $( [2-Aug-2014] $)

  $( Lemma for ~ cxpsqr . $)
  cxpsqrlem $p |- ( ( ( A e. CC /\ A =/= 0 ) /\
    ( A ^c ( 1 / 2 ) ) = -u ( sqr ` A ) ) -> ( _i x. ( sqr ` A ) ) e. RR ) $=
    ( cc wcel cc0 c2 co cfv wceq ci cmul cr cim cre syl sylancr cle wbr wb pire
    cpi wne wa cdiv ccxp csqr cneg ax-icn simpll sqrcl mulcl imval ine0 divcan3
    c1 mp3an23 fveq2d recl 0re letri3 sylancl clog ce ccos rehalfcl ax-mp recni
    1re cioc ccnv cima crn logcl logrn syl6eleq wfn imf ffn elpreima mp2b sylib
    simpld reefcl crp rpefcl rpge0 3syl imcl recoscl cicc immul2 clt w3a simprd
    wf cxr renegcli rexr elioc2 mp2an simp1d recnd mulcom eqtrd simp2d ltle mpd
    wi simp3d elicc2i syl3anbrc halfgt0 elrpii 2ne0 divneg mp3an divreci eqtr2i
    2cn eqcomi iccdili eqeltrd cosq14ge0 mulge0 syl22anc csin caddc cxpef efeul
    mp3an3 resincl addcl remul2 crre oveq2d 3eqtrd breqtrrd adantr simpr mpbird
    syl2anc reneg breqtrd le0neg1 sqrrege0 ad2antrr mpbir2and reim0b ) ABCZADUA
    ZUBZAUNEUCFZUDFZAUEGZUFZHZUBZIUUMJFZKCZUUQLGZDHZUUPUUSUUQIUCFZMGZUUMMGZDUUP
    UUQBCZUUSUVBHUUPIBCZUUMBCZUVDUGUUPUUHUVFUUHUUIUUOUHAUINZIUUMUJOZUUQUKNUUPUV
    AUUMMUUPUVFUVAUUMHZUVGUVFUVEIDUAUVIUGULUUMIUMUONUPUUPUVCDHZUVCDPQZDUVCPQZUU
    PUVCKCZDKCUVJUVKUVLUBRUUPUVFUVMUVGUUMUQNZURUVCDUSUTUUPUVKDUVCUFZPQZUUPDUULM
    GZUVOPUUJDUVQPQUUOUUJDUUKAVAGZJFZMGZVBGZUVSLGZVCGZJFZUVQPUUJUWAKCZDUWAPQZUW
    CKCZDUWCPQZDUWDPQUUJUVTKCZUWEUUJUVSBCZUWIUUJUUKBCZUVRBCZUWJUUKUNKCUUKKCZVGU
    NVDVEZVFZUUJUWLUVRLGZTUFZTVHFZCZUUJUVRLVIUWRVJZCZUWLUWSUBZUUJUVRVAVKUWTAVLV
    MVNBKLWNLBVOUXAUXBRVPBKLVQBUVRUWRLVRVSVTZWAZUUKUVRUJOZUVSUQNZUVTWBNZUUJUWIU
    WAWCCUWFUXFUVTWDUWAWEWFUUJUWBKCZUWGUUJUWJUXHUXEUVSWGNZUWBWHNZUUJUWBTEUCFZUF
    ZUXKWIFZCUWHUUJUWBUWPUUKJFZUXMUUJUWBUUKUWPJFZUXNUUJUWMUWLUWBUXOHUWNUXDUUKUV
    RWJOUUJUWKUWPBCUXOUXNHUWOUUJUWPUUJUWPKCZUWQUWPWKQZUWPTPQZUUJUWSUXPUXQUXRWLZ
    UUJUWLUWSUXCWMUWQWOCZTKCUWSUXSRUWQKCZUXTTSWPZUWQWQVESUWQTUWPWRWSVTZWTZXAUUK
    UWPXBOXCUUJUWPUWQTWIFCZUXNUXMCUUJUXPUWQUWPPQZUXRUYEUYDUUJUXQUYFUUJUXPUXQUXR
    UYCXDUUJUYAUXPUXQUYFXGUYBUYDUWQUWPXEOXFUUJUXPUXQUXRUYCXHUWQTUWPUYBSXIXJUWQT
    UXLUXKUUKUWPUYBSUUKUWNXKXLUXLUWQEUCFZUWQUUKJFTBCEBCEDUAUXLUYGHTSVFZXRXMTEXN
    XOUWQEUWQUYBVFXRXMXPXQUXKTUUKJFTEUYHXRXMXPXSXTNYAUWBYBNUWAUWCYCYDUUJUVQUWAU
    WCIUWBYEGZJFZYFFZJFZMGZUWAUYKMGZJFZUWDUUJUULUYLMUUJUULUVSVBGZUYLUUHUUIUWKUU
    LUYPHUWOAUUKYGYIUUJUWJUYPUYLHUXEUVSYHNXCUPUUJUWEUYKBCZUYMUYOHUXGUUJUWCBCUYJ
    BCZUYQUUJUWCUXJXAUUJUVEUYIBCUYRUGUUJUYIUUJUXHUYIKCZUXIUWBYJNZXAIUYIUJOUWCUY
    JYKYTUWAUYKYLYTUUJUYNUWCUWAJUUJUWGUYSUYNUWCHUXJUYTUWCUYIYMYTYNYOYPYQUUPUVQU
    UNMGZUVOUUPUULUUNMUUJUUOYRUPUUPUVFVUAUVOHUVGUUMUUANXCUUBUUPUVMUVKUVPRUVNUVC
    UUCNYSUUHUVLUUIUUOAUUDUUEUUFYOUUPUVDUURUUTRUVHUUQUUGNYS $.
    $( [2-Aug-2014] $)

  $( The complex exponential function with exponent ` 1 / 2 ` exactly matches
     the complex square root function (the branch cut is in the same place for
     both functions), and thus serves as a suitable generalization to other
     ` n ` -th roots and irrational roots.  (Contributed by Mario Carneiro,
     2-Aug-2014.) $)
  cxpsqr $p |- ( A e. CC -> ( A ^c ( 1 / 2 ) ) = ( sqr ` A ) ) $=
    ( cc wcel c1 c2 co ccxp csqr cfv wceq cc0 wne cr cmul ce cpi caddc cexp syl
    ci cdiv wi 1re rehalfcl ax-mp recni halfgt0 gt0ne0ii 0cxp mp2an sqr0 eqtr4i
    oveq1 fveq2 3eqtr4a a1i cneg clog crp clt wbr ax-icn sqrcl ad2antrr sylancr
    wa sqmul simpll sqrth oveq12d mulm1 3eqtrd cxpsqrlem resqcl eqeltrrd negeq0
    i2 necon3bid biimpa adantr eqnetrd sq0 syl6eq necon3i sqgt0 syl2anc breqtrd
    elrp sylanbrc logneg negneg fveq2d relogcl recnd pire mulcli addcom sylancl
    3eqtr3d oveq2d adddi mp3an12 eqtrd 2ne0 divrec2 mp3an divassi eqtr3i oveq1i
    divcli mulcl efadd efhalfpi negcl syl3anc ax-1cn 2halves oveq2i cxp1 syl5eq
    2cn cxpef rpcxpcl rpcn sqval cxpadd syl211anc eqtr4d 3eqtr4d cle wb rprege0
    rpsqrcl sq11 mpbid eqtr3d mp3an3 rpge0 sqrneg ex mp3an23 cxpcl mpan2 sqeqor
    wo syldan ord con1d pm2.61d pm2.61dne ) ABCZADEUAFZGFZAHIZJZAKAKJZUUOUBUUKU
    UPKUULGFZKHIZUUMUUNUUQKUURUULBCZUULKLUUQKJUULDMCUULMCZUCDUDUEZUFZUULUVAUGUH
    UULUIUJUKULAKUULGUMAKHUNUOUPUUKAKLZUUOUUKUVCVFZUUMUUNUQJZUUOUVDUVEUUOUVDUVE
    VFZUULAURIZNFZOIZTAUQZHIZNFZUUMUUNUVFUVITPEUAFZNFZUULUVJURIZNFZQFZOIZUVNOIZ
    UVPOIZNFZUVLUVFUVHUVQOUVFUVHUULTPNFZNFZUVPQFZUVQUVFUVHUULUWBUVOQFZNFZUWDUVF
    UVGUWEUULNUVFUVJUQZURIZUVOUWBQFZUVGUWEUVFUVJUSCZUWHUWIJUVFUVJMCZKUVJUTVAUWJ
    UVFTUUNNFZERFZUVJMUVFUWMTERFZUUNERFZNFZDUQZANFZUVJUVFTBCUUNBCZUWMUWPJVBUUKU
    WSUVCUVEAVCZVDTUUNVGVEUVFUWNUWQUWOANUWNUWQJUVFVQUPUVFUUKUWOAJZUUKUVCUVEVHZA
    VIZSVJUVFUUKUWRUVJJUXBAVKSVLZUVFUWLMCZUWMMCAVMZUWLVNSVOZUVFKUWMUVJUTUVFUXEU
    WLKLZKUWMUTVAUXFUVFUWMKLUXHUVFUWMUVJKUXDUVDUVJKLZUVEUUKUVCUXIUUKAKUVJKAVPVR
    VSVTZWAUWLKUWMKUWLKJUWMKERFKUWLKERUMWBWCWDSUWLWEWFUXDWGUVJWHWIZUVJWJSUVFUWG
    AURUUKUWGAJUVCUVEAWKVDZWLUVFUVOBCZUWBBCZUWIUWEJUVFUVOUVFUWJUVOMCUXKUVJWMSWN
    ZTPVBPWOUFZWPZUVOUWBWQWRWSWTUVFUXMUWFUWDJZUXOUUSUXNUXMUXRUVBUXQUULUWBUVOXAX
    BSXCUWCUVNUVPQUWBEUAFZUWCUVNUXNEBCEKLUXSUWCJUXQYAXDUWBEXEXFTPEVBUXPYAXDXGXH
    XIWCWLUVFUVNBCUVPBCZUVRUWAJTUVMVBPEUXPYAXDXJWPUVFUUSUXMUXTUVBUXOUULUVOXKVEU
    VNUVPXLVEUVFUVSTUVTUVKNUVSTJUVFXMUPUVFUVJUULGFZUVTUVKUVFUVJBCZUXIUUSUYAUVTJ
    UUKUYBUVCUVEAXNVDZUXJUUSUVFUVBUPZUVJUULYBXOUVFUYAERFZUVKERFZJZUYAUVKJZUVFUV
    JUULUULQFZGFZUVJUYEUYFUVFUYJUVJDGFZUVJUYIDUVJGDBCUYIDJXPDXQUEZXRUVFUYBUYKUV
    JJUYCUVJXSSXTUVFUYEUYAUYANFZUYJUVFUYABCZUYEUYMJUVFUYAUSCZUYNUVFUWJUUTUYOUXK
    UVAUVJUULYCWRZUYAYDSUYAYESUVFUYBUXIUUSUUSUYJUYMJUYCUXJUYDUYDUVJUULUULYFYGYH
    UVFUYBUYFUVJJUYCUVJVISYIUVFUYAMCKUYAYJVAVFZUVKMCKUVKYJVAVFZUYGUYHYKUVFUYOUY
    QUYPUYAYLSUVFUVKUSCZUYRUVFUWJUYSUXKUVJYMSUVKYLSUYAUVKYNWFYOYPVJVLUVDUUMUVIJ
    ZUVEUUKUVCUUSUYTUVBAUULYBYQVTUVFUWGHIZUUNUVLUVFUWGAHUXLWLUVFUWKKUVJYJVAZVUA
    UVLJUXGUVFUWJVUBUXKUVJYRSUVJYSWFYPYIYTUVDUUOUVEUVDUUOUVEUUKUVCUUMERFZUWOJZU
    UOUVEUUEZUVDUUMUUMNFZAVUCUWOUVDAUYIGFZVUFAUVDUUSUUSVUGVUFJUVBUVBAUULUULYFUU
    AUVDVUGADGFZAUYIDAGUYLXRUUKVUHAJUVCAXSVTXTYPUUKVUCVUFJZUVCUUKUUMBCZVUIUUKUU
    SVUJUVBAUULUUBUUCZUUMYESVTUUKUXAUVCUXCVTYIUUKVUDVUEUUKVUJUWSVUDVUEYKVUKUWTU
    UMUUNUUDWFVSUUFUUGUUHUUIYTUUJ $.
    $( [2-Aug-2014] $)

