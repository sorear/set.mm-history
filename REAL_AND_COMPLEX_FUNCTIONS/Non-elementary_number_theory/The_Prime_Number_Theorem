$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          The Prime Number Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k K $.  $d k m n N $.
    chebbnd1lem1.1 $e |- K = if ( ( 2 x. N ) <_ ( ( 2 x. N ) _C N ) ,
      ( 2 x. N ) , ( ( 2 x. N ) _C N ) ) $.
    $( Lemma for ~ chebbnd1 : show a lower bound on ` ppi ( x ) ` at even
       integers using similar techniques to those used to prove ~ bpos .  (Note
       that the expression ` K ` is actually equal to ` 2 x. N ` , but proving
       that is not necessary for the proof, and it's too much work.)  The key
       to the proof is ~ bposlem1 , which shows that each term in the expansion
       ` ( ( 2 x. N ) _C N ) = prod_ p e. Prime `
       ` ( p ^ ( p pCnt ( ( 2 x. N ) _C N ) ) ) ` is at most ` 2 x. N ` , so
       that the sum really only has nonzero elements up to ` 2 x. N ` , and
       since each term is at most ` 2 x. N ` , after taking logs we get the
       inequality ` ppi ( 2 x. N ) x. log ( 2 x. N ) <_ `
       ` log ( ( 2 x. N ) _C N ) ` , and ~ bclbnd finishes the proof. $)
    chebbnd1lem1 $p |- ( N e. ( ZZ>= ` 4 ) -> ( log ` ( ( 4 ^ N ) / N ) ) <
      ( ( ppi ` ( 2 x. N ) ) x. ( log ` ( 2 x. N ) ) ) ) $=
      ( vk cfv wcel co clog c2 crp cr cn c1 syl syl2anc wbr cle cprime wa wceq
      vn vm c4 cuz cexp cdiv cmul cbc cppi cn0 nnuz uztrn2 mpan nnexpcl sylancr
      4nn nnnn0 nnrp rpdivcl relogcl cvv cc0 cfz ovex fzctr bccl2 cz 2z eluzelz
      zmulcl zre ppicl nn0re 2nn nnmulcl remulcl clt bclbnd wb logltb mpbid cif
      ifcl syl5eqel nnre cin cv cpc csu cfn wss fzfid ssfi sylancl nnz syl5eqbr
      inss1 eluz2 syl3anbrc fzss2 ssrin sselda simpr sseldi elfznn inss2 adantr
      min2 pccl syldan ce elin simprbi bposlem1 syl2an reeflog cc recnd adantrr
      wn adantl nnge1 syl6eleq syl3anc letrd mpbir2and wo sylib log1 caddc cmpt
      fveq2d cseq eqid fvmpt 1nn a1i eqtr4d 2re ltletrd efle mpbird fsumle cdif
      3brtr4d eldifn difss nncn exp1 simprr expwordi eqbrtrrd elfzle2 syl6breqr
      lemin fznn sylanbrc expr mtod elnn0 ord mpd oveq2d exp0 eqtrd syl6eq wral
      fsumss ralrimiva fzfi olci sumss2 mpan2 baib ifbid fvif ifeq2 ax-mp eqtri
      syl6eqr sumeq2i eleq1 oveq1 oveq12d eqidd ifbieq12d fvex ad2antrr fsumser
      id ifclda rpmulcl elexi ifex eqeltrd seqhomo pcprod 3eqtr2d syl5eq 3eqtrd
      relogmul fsumconst eleqtri ppival2g oveq1d 3brtr3d min1 ppiwordi 1re 1lt2
      chash mulid1i eluzelre 2pos pm3.2i lemul2 syl5eqbrr rplogcl elrp lemul1
      2cn ) BUCUDEFZUCBUEGZBUFGZHEZIBUGGZBUHGZHEZUYFUIEZUYFHEZUGGZUYBUYDJFZUYEK
      FUYBUYCJFZBJFZUYLUYBUYCLFZUYMUYBUCLFZBUJFZUYOUPUYBBLFZUYQUYPUYBUYRUPMBUCL
      UKULUMZBUQNZUCBUNUOUYCURNUYBUYRUYNUYSBURNUYCBUSOZUYDUTNUYBUYGJFZUYHKFUYBU
      YGLFZVUBUYBUYFVAFBVBUYFVCGFZVUCIBUGVDUYBUYQVUDUYTBVENBUYFVAVFUOZUYGURNZUY
      GUTNZUYBUYIKFZUYJKFZUYKKFUYBUYIUJFZVUHUYBUYFKFZVUJUYBUYFVGFZVUKUYBIVGFBVG
      FVULVHUCBVIIBVJUOUYFVKNZUYFVLNUYIVMNZUYBUYFJFZVUIUYBUYFLFZVUOUYBILFUYRVUP
      VNUYSIBVOUOZUYFURNZUYFUTNZUYIUYJVPOZUYBUYDUYGVQPZUYEUYHVQPZBVRUYBUYLVUBVV
      AVVBVSVUAVUFUYDUYGVTOWAUYBUYHAUIEZUYJUGGZUYKVUGUYBVVCKFZVUIVVDKFUYBVVCUJF
      ZVVEUYBAKFZVVFUYBALFZVVGUYBAUYFUYGQPZUYFUYGWBZLCUYBVUPVUCVVJLFVUQVUEVVIUY
      FUYGLWCOWDZAWENZAVLNVVCVMNZVUSVVCUYJVPOVUTUYBMAVCGZRWFZDWGZVVPUYGWHGZUEGZ
      HEZDWIZVVOUYJDWIZUYHVVDQUYBVVOVVSUYJDUYBVVNWJFVVOVVNWKVVOWJFZUYBMAWLVVNRW
      QVVNVVOWMWNZUYBVVPVVOFZVVPMUYGVCGZRWFZFZVVSKFZUYBVVOVWFVVPUYBVVNVWEWKZVVO
      VWFWKUYBUYGAUDEFZVWIUYBAVGFZUYGVGFZAUYGQPVWJUYBVVHVWKVVKAWONZUYBVUCVWLVUE
      UYGWONUYBAVVJUYGQCUYBVUKUYGKFZVVJUYGQPVUMUYBVUCVWNVUEUYGWENZUYFUYGXHOWPAU
      YGWRWSAMUYGWTNVVNVWERXANZXBZUYBVWGSZVVRJFZVWHVWRVVRLFZVWSVWRVVPLFZVVQUJFZ
      VWTVWRVVPVWEFZVXAVWRVWFVWEVVPVWERWQZUYBVWGXCZXDVVPUYGXEZNVWRVVPRFZVUCVXBV
      WRVWFRVVPVWERXFZVXEXDUYBVUCVWGVUEXGVVPUYGXIZOZVVPVVQUNZOZVVRURNZVVRUTNZXJ
      ZUYBVUIVWDVUSXGZUYBVWDSZVVSUYJQPZVVSXKEZUYJXKEZQPZVXQVVRUYFVXSVXTQUYBUYRV
      XGVVRUYFQPZVWDUYSVWDVVPVVNFZVXGVVPVVNRXLZXMVVPBXNZXOVXQVWSVXSVVRTUYBVWDVW
      GVWSVWQVXMXJVVRXPNVXQVUOVXTUYFTUYBVUOVWDVURXGUYFXPNUUEVXQVWHVUIVXRVYAVSVX
      OVXPVVSUYJUUAOUUBUUCUYBVVTVWFVVSDWIZVWEVWGVVSVBWBZDWIZUYHUYBVVOVWFVVSDVWP
      UYBVWDVWGVVSXQFZVWQVWRVVSVXNXRZXJUYBVVPVWFVVOUUDZFZSZVVSMHEZVBVYMVVRMHVYM
      VVRVVPVBUEGZMVYMVVQVBVVPUEVYMVVQLFZXTVVQVBTZVYMVYPVWDVYLVWDXTUYBVVPVWFVVO
      UUFYAUYBVYLVYPVWDUYBVYLVYPSZSZVYCVXGVWDVYSVYCVXAVVPAQPZUYBVYLVXAVYPVYMVXC
      VXAVYMVWFVWEVVPVXDVYMVYKVWFVVPVWFVVOUUGUYBVYLXCXDZXDZVXFNZXSZVYSVVPVVJAQV
      YSVVPVVJQPZVVPUYFQPZVVPUYGQPZVYSVVPVVRUYFVYSVXAVVPKFZWUDVVPWENZUYBVYLVVRK
      FZVYPVYMVWTWUJUYBVYLVWGVWTWUAVXLXJVVRWENXSUYBVUKVYRVUMXGZVYSVVPMUEGZVVPVV
      RQVYSVVPXQFZWULVVPTVYSVXAWUMWUDVVPUUHZNVVPUUINVYSWUHMVVPQPZVVQMUDEZFWULVV
      RQPWUIVYSVXAWUOWUDVVPYBNVYSVVQLWUPUYBVYLVYPUUJUKYCVVPMVVQUUKYDUULVYSUYRVX
      GVYBUYBUYRVYRUYSXGUYBVYLVXGVYPVYMVWFRVVPVXHWUAXDXSZVYEOYEUYBVYLWUGVYPVYMV
      XCWUGWUBVVPMUYGUUMNXSVYSWUHVUKVWNWUEWUFWUGSVSWUIWUKUYBVWNVYRVWOXGVVPUYFUY
      GUUOYDYFCUUNVYSVVHVYCVXAVYTSVSUYBVVHVYRVVKXGVVPAUUPNYFWUQVYDUUQUURUUSVYMV
      YPVYQVYMVXBVYPVYQYGUYBVYLVWGVXBWUAVXJXJVVQUUTYHUVAUVBUVCVYMWUMVYOMTVYMVXA
      WUMWUCWUNNVVPUVDNUVEYLYIUVFUYBVWEWJFZVWFVWEWKZVWFWJFUYBMUYGWLVXDVWEVWFWMW
      NUVHUYBWUSVYIDVWFUVGZVYFVYHTZVXDUYBVYIDVWFVYJUVIWUSWUTSVWEWUPWKZWURYGWVAW
      URWVBMUYGUVJUVKVWFVWEVVSDMUVLUVMUOUYBVYHVWEVXGVVRMWBZHEZDWIZUYHVWEVYGWVDD
      VXCVYGVXGVVSVBWBZWVDVXCVWGVXGVVSVBVWGVXCVXGVVPVWERXLUVNUVOWVDVXGVVSVYNWBZ
      WVFVXGVVRMHUVPVYNVBTWVGWVFTYIVXGVYNVBVVSUVQUVRUVSUVTUWAUYBWVEUYGYJUALUAWG
      ZRFZWVHWVHUYGWHGZUEGZMWBZHEZYKZMYMEUYGUGUALWVLYKZMYMEZHEUYHUYBWVDDWVNMUYG
      UYBVXCSZVXAVVPWVNEZWVDTVXCVXAUYBVXFYAZUAVVPWVMWVDLWVNWVHVVPTZWVLWVCHWVTWV
      IVXGWVKMVVRMWVHVVPRUWBWVTWVHVVPWVJVVQUEWVTUWJWVHVVPUYGWHUWCUWDWVTMUWEUWFZ
      YLWVNYNWVCHUWGYONZUYBUYGLWUPVUEUKYCZWVQWVDWVQWVCJFZWVDKFWVQWVCLFWWDWVQVXG
      VVRMLWVQVXGSZVXAVXBVWTWVQVXAVXGWVSXGWWEVXGVUCVXBWVQVXGXCUYBVUCVXCVXGVUEUW
      HVXIOVXKOMLFWVQVXGXTSYPYQUWKWVCURNZWVCUTNXRUWIUYBDUBUGYJJWVOWVNHMUYGVVPJF
      UBWGZJFSZVVPWWGUGGZJFUYBVVPWWGUWLYAWVQVVPWVOEZWVCJWVQVXAWWJWVCTWVSUAVVPWV
      LWVCLWVOWWAWVOYNZVXGVVRMVVPVVQUEVDMLYPUWMUWNYONZWWFUWOWWCWWHWWIHEVVPHEWWG
      HEYJGTUYBVVPWWGUXAYAWVQWWJHEWVDWVRWVQWWJWVCHWWLYLWWBYRUWPUYBWVPUYGHUYBVUC
      WVPUYGTVUEUAWVOUYGWWKUWQNYLUWRUWSUWTUYBVWAVVOUXKEZUYJUGGZVVDUYBVWBUYJXQFV
      WAWWNTVWCUYBUYJVUSXRVVOUYJDUXBOUYBVVCWWMUYJUGUYBVWKIWUPFVVCWWMTVWMILWUPVN
      UKUXCAMUXDWNUXEYRUXFUYBVVCUYIQPZVVDUYKQPZUYBVVGVUKAUYFQPWWOVVLVUMUYBAVVJU
      YFQCUYBVUKVWNVVJUYFQPVUMVWOUYFUYGUXGOWPAUYFUXHYDUYBVVEVUHVUIVBUYJVQPSZWWO
      WWPVSVVMVUNUYBUYJJFZWWQUYBVUKMUYFVQPWWRVUMUYBMIUYFMKFZUYBUXIYQZIKFZUYBYSY
      QVUMMIVQPUYBUXJYQUYBIIMUGGZUYFQIUYAUXLUYBMBQPZWXBUYFQPZUYBUYRWXCUYSBYBNUY
      BWWSBKFWXAVBIVQPZSZWXCWXDVSWWTUCBUXMWXFUYBWXAWXEYSUXNUXOYQMBIUXPYDWAUXQYT
      UYFUXROUYJUXSYHVVCUYIUYJUXTYDWAYEYT $.
      $( [22-Sep-2014] $)
  $}

  ${
    chebbnd1lem2.1 $e |- M = ( |_ ` ( N / 2 ) ) $.
    $( Lemma for ~ chebbnd1 :  Show that ` log ( N ) / N ` does not change too
       much between ` N ` and ` M = |_ ( N / 2 ) ` . $)
    chebbnd1lem2 $p |- ( ( N e. RR /\ 8 <_ N ) ->
      ( ( log ` ( 2 x. M ) ) / ( 2 x. M ) ) < ( 2 x. ( ( log ` N ) / N ) ) ) $=
      ( cr wcel cle wbr c2 co cdiv c4 a1i syl cc0 clt wb mpbid c1 sylancr ceu
      c8 wa cmul clog cfv crp 2re 2pos elrpii cn cuz 4nn cz cfl rehalfcl adantr
      nnzi flcl syl5eqel 4t2e8 simpr syl5eqbr 4re simpl lemuldiv syl112anc flge
      sylancl syl6breqr syl3anbrc nnuz uztrn2 rpmulcl relogcl rerpdivcl syl2anc
      eluz2 nnrp 0re 8pos ltletrd elrp sylanbrc rphalfcl remulcl caddc peano2re
      8re zre flltp1 oveq1i nnge1 1re leadd2 syl3anc wceq recnd 2times breqtrrd
      cc ere c3 egt2lt3 simpri 3lt4 lttri mp2an wi ltle lttrd logdivlt syl22anc
      3re mpd rphalflt logltb rpgt0 ltdiv1 wne rpne0 2ne0 divdiv2 mulcom oveq1d
      syl122anc divass 3eqtrd breqtrd ) BDEZUABFGZUBZHAUCIZUDUEZYLJIZBHJIZUDUEZ
      YOJIZHBUDUEZBJIZUCIZYKYMDEZYLUFEZYNDEYKUUBUUAYKHUFEAUFEZUUBHUGUHUIYKAUJEZ
      UUCYKKUJEAKUKUEEZUUDULYKKUMEZAUMEZKAFGUUEUUFYKKULUQZLYKAYOUNUEZUMCYKYODEZ
      UUIUMEYIUUJYJBUOUPZYOURMUSZYKKUUIAFYKKYOFGZKUUIFGZYKKHUCIZBFGZUUMYKUUOUAB
      FUTYIYJVAZVBYKKDEZYIHDEZNHOGZUUPUUMPUURYKVCLZYIYJVDZUUSYKUGLZUUTYKUHLKBHV
      EVFQZYKUUJUUFUUMUUNPUUKUUHYOKVGVHQCVIKAVQVJRAKUJVKVLSZAVRMHAVMSZYLVNMUVFY
      MYLVOVPYKYPDEZYOUFEZYQDEYKUVHUVGYKBUFEZUVHYKYINBOGUVIUVBYKNUABNDEYKVSLUAD
      EYKWHLUVBNUAOGYKVTLUUQWABWBWCZBWDMZYOVNMZUVKYPYOVOVPYKUUSYSDEZYTDEUGYKYRD
      EZUVIUVMYKUVIUVNUVJBVNMZUVJYRBVOVPHYSWESYKYOYLOGZYNYQOGZYKYOARWFIZYLUUKYK
      ADEZUVRDEYKUUGUVSUULAWIMZAWGMYKUUSUVSYLDEZUGUVTHAWESZYKYOUUIRWFIZUVROYKUU
      JYOUWCOGUUKYOWJMAUUIRWFCWKVIYKUVRAAWFIZYLFYKRAFGZUVRUWDFGZYKUUDUWEUVEAWLM
      YKRDEZUVSUVSUWEUWFPUWGYKWMLUVTUVTRAAWNWOQYKAWTEYLUWDWPYKAUVTWQAWRMWSWAZYK
      UUJTYOFGZUWATYLFGZUVPUVQPUUKYKTYOOGZUWIYKTKYOTDEZYKXALZUVAUUKTKOGZYKTXBOG
      ZXBKOGUWNHTOGUWOXCXDXETXBKXAXMVCXFXGLUVDWAZYKUWLUUJUWKUWIXHXAUUKTYOXISXNU
      WBYKTYLOGZUWJYKTYOYLUWMUUKUWBUWPUWHXJYKUWLUWAUWQUWJXHXAUWBTYLXISXNYOYLXKX
      LQYKYQYRYOJIZYTOYKYPYROGZYQUWROGZYKYOBOGZUWSYKUVIUXAUVJBXOMYKUVHUVIUXAUWS
      PUVKUVJYOBXPVPQYKUVGUVNUUJNYOOGZUWSUWTPUVLUVOUUKYKUVHUXBUVKYOXQMYPYRYOXRV
      FQYKUWRYRHUCIZBJIZHYRUCIZBJIZYTYKYRWTEZBWTEZBNXSZHWTEZHNXSZUWRUXDWPYKYRUV
      OWQZYKBUVBWQZYKUVIUXIUVJBXTMZYKHUVCWQZUXKYKYALYRBHYBYEYKUXCUXEBJYKUXGUXJU
      XCUXEWPUXLUXOYRHYCVPYDYKUXJUXGUXHUXIUXFYTWPUXOUXLUXMUXNHYRBYFVFYGYHXJ $.
      $( [22-Sep-2014] $)

    $( Lemma for ~ chebbnd1 : get an upper bound on
       ` ppi ( N ) x. ( N / log ( N ) ) ` that is independent of ` N ` . $)
    chebbnd1lem3 $p |- ( ( N e. RR /\ 8 <_ N ) ->
      ( ( ( log ` 2 ) - ( 1 / ( 2 x. _e ) ) ) / 2 ) <
      ( ( ppi ` N ) x. ( ( log ` N ) / N ) ) ) $=
      ( cr wcel cle wbr c2 c1 ceu cmul co cdiv a1i syl clt c4 cc0 wb syl2anc c8
      wa clog cfv cmin cppi crp 2re 2pos elrpii relogcl ax-mp 1re remulcli epos
      ere mulgt0ii gt0ne0ii redivcli resubcli 2ne0 8re simpl ltleii simpr letrd
      cn 2lt8 ppinncl syldan nnre cfl rehalfcl adantr flcl syl5eqel zre remulcl
      cz sylancr 1lt2 2cn mulid1i cuz 4nn 4t2e8 syl5eqbr 4re lemuldiv syl112anc
      nnzi mpbid flge sylancl syl6breqr eluz2 syl3anbrc uztrn2 lemul2 syl5eqbrr
      nnuz ltletrd rplogcl rpre 2nn nnmulcl nndivre 0re 8pos sylanbrc rerpdivcl
      nnge1 elrp cexp 4pos rpexpcl nnrp rpdivcl egt2lt3 simpri 3lt4 lttri mp2an
      c3 3re wi oveq1i nngt0 mpbird cc recnd breqtrd ltmuldiv syl3anc recni wne
      wceq cn0 lttrd sylib ltle mpd leidi logdivlt mpanl12 loge syl6breq pm3.2i
      jca lt2mul2div syl22anc mulid2 epr ltsub2 rpcnne0 simpld subdir mulcom 2z
      zmulcl relogexp 2nn0 nnnn0 expmul syl6eq fveq2d 3eqtr2d divrec2 syl122anc
      sq2 divcan5 eqtr3d oveq12d eqtrd relogdiv 3brtr4d cbc chebbnd1lem1 divass
      cif eqid flle lemuldiv2 ppiwordi lemul1 ltdiv1 chebbnd1lem2 ltmul2 mul12
      ltdivmul ) BDEZUABFGZUBZHUCUDZIHJKLZMLZUELZHMLZBUFUDZHAKLZUCUDZUWTMLZKLZH
      MLZUWSBUCUDZBMLZKLZUWRDEUWMUWQHUWNUWPHUGEZUWNDEHUHUIUJZHUKULZIUWOUMHJUHUP
      UNZUWOUXKHJUHUPUIUOUQURZUSZUTZUHVAUSNUWMUXCDEZUXDDEUWMUWSDEZUXBDEZUXOUWMU
      WSVGEZUXPUWKUWLHBFGUXRUWMHUABHDEZUWMUHNZUADEUWMVBNZUWKUWLVCZHUAFGUWMHUAUH
      VBVHVDNUWKUWLVEZVFBVIVJZUWSVKOZUWMUXADEZUWTVGEZUXQUWMUXAUGEZUYFUWMUWTDEZI
      UWTPGUYHUWMUXSADEZUYIUHUWMAVSEZUYJUWMABHMLZVLUDZVSCUWMUYLDEZUYMVSEUWKUYNU
      WLBVMVNZUYLVOOVPZAVQOZHAVRVTZUWMIHUWTIDEZUWMUMNZUXTUYRIHPGUWMWANUWMHHIKLZ
      UWTFHWBWCUWMIAFGZVUAUWTFGZUWMAVGEZVUBUWMQVGEAQWDUDEZVUDWEUWMQVSEZUYKQAFGV
      UEVUFUWMQWEWKZNUYPUWMQUYMAFUWMQUYLFGZQUYMFGZUWMQHKLZBFGZVUHUWMVUJUABFWFUY
      CWGUWMQDEZUWKUXSRHPGZVUKVUHSVULUWMWHNZUYBUXTVUMUWMUINZQBHWIWJWLUWMUYNVUFV
      UHVUISUYOVUGUYLQWMWNWLCWOZQAWPWQZIAQVGXAWRVTZAXLOUWMUYSUYJUXSVUMVUBVUCSUY
      TUWMVUDUYJVURAVKOUXTVUOIAHWSWJWLWTZXBUWTXCTZUXAXDOZUWMHVGEVUDUYGXEVURHAXF
      VTZUXAUWTXGTZUWSUXBVRTZUXCVMOUWMUXPUXFDEZUXGDEZUYEUWMUXEDEZBUGEZVVEUWMVVH
      VVGUWMUWKRBPGVVHUYBUWMRUABRDEUWMXHNUYAUYBRUAPGUWMXINUYCXBBXMXJZBUKOVVIUXE
      BXKTZUWSUXFVRTZUWMUWQUXCPGZUWRUXDPGZUWMUWQUWTUFUDZUXBKLZUXCUWQDEZUWMUXNNZ
      UWMVVNDEZUXQVVODEUWMVVNVGEZVVRUWMUYIHUWTFGVVSUYRVUSUWTVITVVNVKOZVVCVVNUXB
      VRTVVDUWMUWQVVNUXAKLZUWTMLZVVOPUWMUWQUWTKLZVWAPGZUWQVWBPGZUWMVWCQAXNLZAML
      ZUCUDZVWAUWMVVPUYIVWCDEUXNUYRUWQUWTVRVTUWMVWGUGEZVWHDEUWMVWFUGEZAUGEZVWIU
      WMQUGEUYKVWJQWHXOUJUYPQAXPVTZUWMVUDVWKVURAXQOZVWFAXRTVWGUKOUWMVVRUYFVWADE
      ZVVTVVAVVNUXAVRTZUWMVWFUCUDZAJMLZUELZVWPAUCUDZUELZVWCVWHPUWMVWSVWQPGZVWRV
      WTPGZUWMVWSJKLZAPGZVXAUWMVXCIAKLZAPUWMVXCVXEPGZVWSAMLZIJMLZPGZUWMVXGJUCUD
      ZJMLZVXHPUWMJAPGZVXGVXKPGZUWMJQAJDEZUWMUPNVUNUYQJQPGZUWMJYDPGZYDQPGVXOHJP
      GVXPXSXTYAJYDQUPYEWHYBYCNVUPXBZUWMUYJJAFGZVXLVXMSZUYQUWMVXLVXRVXQUWMVXNUY
      JVXLVXRYFUPUYQJAUUAVTUUBVXNJJFGUYJVXRUBVXSUPJUPUUCJAUUDUUETWLVXJIJMUUFYGU
      UGUWMVWSDEZVXNRJPGZUBZUYSUYJRAPGZUBVXFVXISUWMVWKVXTVWMAUKOZVYBUWMVXNVYAUP
      UOUUHNZUYTUWMUYJVYCUYQUWMVUDVYCVURAYHOUUIVWSJIAUUJUUKYIUWMAYJEZVXEAYQUWMA
      UYQYKZAUULOYLUWMVXTUYJVYBVXDVXASVYDUYQVYEVWSAJYMYNWLUWMVXTVWQDEZVWPDEZVXA
      VXBSVYDUWMUYJJUGEVYHUYQUUMAJXKWNUWMVWJVYIVWLVWFUKOVWSVWQVWPUUNYNWLUWMVWCU
      WNUWTKLZUWPUWTKLZUELZVWRUWMUWNYJEZUWPYJEZUWTYJEZVWCVYLYQVYMUWMUWNUXJYONZV
      YNUWMUWPUXMYONUWMVYOUWTRYPZUWMUWTUGEZVYOVYQUBZUWMUYGVYRVVBUWTXQOZUWTUUOOZ
      UUPZUWNUWPUWTUUQYNUWMVYJVWPVYKVWQUEUWMVYJUWTUWNKLZHUWTXNLZUCUDZVWPUWMVYMV
      YOVYJWUCYQVYPWUBUWNUWTUURTUWMUXHUWTVSEZWUEWUCYQUXIUWMHVSEUYKWUFUUSUYPHAUU
      TVTHUWTUVAVTUWMWUDVWFUCUWMWUDHHXNLZAXNLZVWFUWMHYJEZHYREZAYREZWUDWUHYQWUIU
      WMWBNZWUJUWMUVBNUWMVUDWUKVURAUVCOHHAUVDYNWUGQAXNUVJYGUVEUVFUVGUWMUWTUWOML
      ZVYKVWQUWMVYOUWOYJEZUWORYPZWUMVYKYQWUBWUNUWMUWOUXKYONWUOUWMUXLNUWTUWOUVHY
      NUWMVYFJYJEZJRYPZWUIHRYPZWUMVWQYQVYGWUPUWMJUPYONWUQUWMJUPUOURNWULWURUWMVA
      NAJHUVKUVIUVLUVMUVNUWMVWJVWKVWHVWTYQVWLVWMVWFAUVOTUVPUWMVUEVWHVWAPGVUQUWT
      UWTAUVQLZFGUWTWUSUVTZAWUTUWAUVROYSUWMVVPVWNUYIRUWTPGUBZVWDVWESVVQVWOUWMVY
      RWVAVYTUWTXMYTUWQVWAUWTYMYNWLUWMVVNYJEUXAYJEVYSVWBVVOYQUWMVVNVVTYKUWMUXAV
      VAYKWUAVVNUXAUWTUVSYNYLUWMVVNUWSFGZVVOUXCFGZUWMUYIUWKUWTBFGZWVBUYRUYBUWMW
      VDAUYLFGZUWMAUYMUYLFCUWMUYNUYMUYLFGUYOUYLUWBOWGUWMUYJUWKUXSVUMWVDWVESUYQU
      YBUXTVUOABHUWCWJYIUWTBUWDYNUWMVVRUXPUXQRUXBPGUBZWVBWVCSVVTUYEUWMUXBUGEZWV
      FUWMUYHVYRWVGVUTVYTUXAUWTXRTUXBXMYTVVNUWSUXBUWEYNWLXBUWMVVPUXOUXSVUMVVLVV
      MSVVQVVDUXTVUOUWQUXCHUWFWJWLUWMUXDUXGPGZUXCHUXGKLZPGZUWMUXCUWSHUXFKLZKLZW
      VIPUWMUXBWVKPGZUXCWVLPGZABCUWGUWMUXQWVKDEZUXPRUWSPGZWVMWVNSVVCUWMUXSVVEWV
      OUHVVJHUXFVRVTUYEUWMUXRWVPUYDUWSYHOUXBWVKUWSUWHWJWLUWMUWSYJEWUIUXFYJEWVLW
      VIYQUWMUWSUYEYKWULUWMUXFVVJYKUWSHUXFUWIYNYLUWMUXOVVFUXSVUMWVHWVJSVVDVVKUX
      TVUOUXCUXGHUWJWJYIYS $.
      $( [21-Sep-2014] $)
  $}

  $( The Chebyshev bound:  The function ` ppi ( x ) ` is eventually lower
     bounded by a positive constant times ` x / log ( x ) ` .  Alternatively
     stated, the function ` ( x / log ( x ) ) / ppi ( x ) ` is eventually
     bounded.  (Contributed by Mario Carneiro, 22-Sep-2014.) $)
  chebbnd1 $p |- ( x e. ( 2 [,) +oo ) |->
    ( ( x / ( log ` x ) ) / ( ppi ` x ) ) ) e. O(1) $=
    ( c2 co cfv cdiv wcel c1 ceu cr 2re mp2an a1i cc crp cc0 clt wbr syl wceq
    c4 cpnf cico cv clog cppi cmpt co1 wtru cmul cmin wss cxr pnfxr icossre cle
    c8 wa wb elicopnf ax-mp simplbi 0re 1re lt01 simprbi ltletrd lttrd sylanbrc
    1lt2 elrp rplogcl syl2anc rpdivcl ppinncl sylbi nnrp rpcn adantl 8re elrpii
    cn 2pos relogcl ere remulcli epos mulgt0ii gt0ne0ii rereccli resubcli recni
    mulid1i c3 egt2lt3 simpli ltmul2i mpbi eqbrtrri ltrecii simpri 3lt4 3re 4re
    lttri epr 4pos logltb loge cexp fveq2i cz 2z relogexp eqtr3i 3brtr3i pm3.2i
    sq2 ltdivmul mp3an mpbir rehalfcl posdifi rerpdivcl cabs rpge0 absid adantr
    rpre cfl eqid chebbnd1lem3 sylan 2ne0 recdiv mp4an rpne0 nncn nnne0 rpcnne0
    wne syl22anc divrec syl3anc oveq2d 3eqtrd 3brtr4d divgt0ii ltrec mpanr12 wi
    mpbird ltle sylancl mpd eqbrtrd elo1d trud ) ABUAUBCZAUCZUUSUDDZECZUUSUEDZE
    CZUFUGFUHAUURUVCUPBBUDDZGBHUICZECZUJCZECZUURIUKZUHBIFZUAULFUVIJUMBUAUNKLUUS
    UURFZUVCMFZUHUVKUVCNFZUVLUVKUVANFZUVBNFZUVMUVKUUSNFZUUTNFZUVNUVKUUSIFZOUUSP
    QUVPUVKUVRBUUSUOQZUVJUVKUVRUVSUQZURJBUUSUSUTZVAZUVKOGUUSOIFUVKVBLGIFZUVKVCL
    ZUWBOGPQUVKVDLUVKGBUUSUWDUVJUVKJLUWBGBPQZUVKVILUVKUVRUVSUWAVEVFZVGUUSVJVHZU
    VKUVRGUUSPQUVQUWBUWFUUSVKVLZUUSUUTVMVLZUVKUVBWAFZUVOUVKUVTUWJUWAUUSVNVOZUVB
    VPRUVAUVBVMVLZUVCVQRVRUPIFUHVSLUVHIFZUHUVJUVGNFUWMJUVGUVDUVFBNFZUVDIFZBJWBV
    TZBWCUTZUVEBHJWDWEZUVEUWRBHJWDWBWFWGZWHWIZWJZUVFUVDPQZOUVGPQUVFGBECZPQZUXCU
    VDPQZUXBBUVEPQUXDBGUICZBUVEPBBJWKZWLGHPQZUXFUVEPQZUWEBHPQZUXHVIUXJHWMPQZWNW
    OGBHVCJWDXDKOBPQZUXHUXIURWBGHBVCWDJWPUTWQWRBUVEJUWRWBUWSWSWQUXEGBUVDUICZPQZ
    HUDDZTUDDZGUXMPHTPQZUXOUXPPQZUXKWMTPQUXQUXJUXKWNWTXAHWMTWDXBXCXDKHNFTNFUXQU
    XRURXETXCXFVTHTXGKWQXHBBXICZUDDZUXPUXMUXSTUDXQXJUWNBXKFUXTUXMSUWPXLBBXMKXNX
    OUWCUWOUVJUXLUQUXEUXNURVCUWQUVJUXLJWBXPGUVDBXRXSXTUVFUXCUVDUWTUWCUXCIFVCGYA
    UTUWQXDKUVFUVDUWTUWQYBWQZVTBUVGYCKZLUVKUPUUSUOQZUQZUVCYDDZUVHUOQUHUYDUYEUVC
    UVHUOUVKUYEUVCSZUYCUVKUVMUYFUWLUVMUVCIFZOUVCUOQUYFUVCYHZUVCYEUVCYFVLRYGUYDU
    VCUVHPQZUVCUVHUOQZUYDUYIGUVHECZGUVCECZPQZUYDUVGBECZUVBUUTUUSECZUICZUYKUYLPU
    VKUVRUYCUYNUYPPQUWBUUSBECYIDZUUSUYQYJYKYLUYKUYNSZUYDBMFBOYTUVGMFUVGOYTUYRUX
    GYMUVGUXAWKUVGUXAUYAWHBUVGYNYOLUVKUYLUYPSUYCUVKUYLUVBUVAECZUVBGUVAECZUICZUY
    PUVKUVAMFZUVAOYTZUVBMFZUVBOYTZUYLUYSSUVKUVNVUBUWIUVAVQRZUVKUVNVUCUWIUVAYPRZ
    UVKUWJVUDUWKUVBYQRZUVKUWJVUEUWKUVBYRRUVAUVBYNUUAUVKVUDVUBVUCUYSVUASVUHVUFVU
    GUVBUVAUUBUUCUVKUYTUYOUVBUIUVKUUSMFUUSOYTUQZUUTMFUUTOYTUQZUYTUYOSUVKUVPVUIU
    WGUUSYSRUVKUVQVUJUWHUUTYSRUUSUUTYNVLUUDUUEYGUUFUYDUVMUYIUYMURZUVKUVMUYCUWLY
    GZUVMUYGOUVCPQUQZVUKUVCVJVUMUWMOUVHPQVUKUYBBUVGJUXAWBUYAUUGUVCUVHUUHUUIVORU
    UKUYDUYGUWMUYIUYJUUJUYDUVMUYGVULUYHRUYBUVCUVHUULUUMUUNUUOVRUUPUUQ $.
    $( [21-Sep-2014] $)

  ${
    $d p x z A $.  $d p N $.  $d p x z ph $.
    chtppilim.1 $e |- ( ph -> A e. RR+ ) $.
    chtppilim.2 $e |- ( ph -> A < 1 ) $.
    ${
      chtppilim.3 $e |- ( ph -> N e. ( 2 [,) +oo ) ) $.
      chtppilim.4 $e |- ( ph -> ( ( N ^c A ) / ( ppi ` N ) ) < ( 1 - A ) ) $.
      $( Lemma for ~ chtppilim . $)
      chtppilimlem1 $p |- ( ph ->
        ( ( A ^ 2 ) x. ( ( ppi ` N ) x. ( log ` N ) ) ) < ( theta ` N ) ) $=
        ( vp c2 co cfv clt wcel wceq cr syl cle wbr syl2anc c1 cexp cppi cc crp
        clog cmul ccht rpre recnd sqval oveq1d cn0 cpnf cico wa wb 2re elicopnf
        ax-mp sylib simpld ppicl nn0re 3syl cc0 0re a1i simprd ltletrd sylanbrc
        2pos elrp relogcl mul4 syl22anc eqtrd ccxp cmin remulcl rpcxpcl resubcl
        chtcl cdiv ppinncl nnrp rerpdivcl 1re ltsub13 syl3anc mpbid wne rpcnne0
        cn divsubdir divid breqtrrd ltmuldiv mpbird ppiltx ltsub2 lttrd rplogcl
        1lt2 rpmulcl ltmul1 cfl caddc cfz cprime cin cv csu cfn wss fzfid inss1
        sylancl inss2 simpr sseldi prmnn fsumrecl chash fsumconst ppifl oveq12d
        ssfi cuz cz flcl wi ltle sylancr mpd cxplea adantr ce letrd rpge0 nnuz
        syl221anc breqtrd flwordi eluz2 syl3anbrc ppidif eqtr3d eqtr4d peano2re
        cxp1 reflcl flltp1 elfzle1 rpne0 eqcomd reeflog 3brtr4d fsumle eqbrtrrd
        cxpef efle prmuz2 eluz2b2 nnre flge0nn0 nn0p1nn syl6eleq fzss1 fsumless
        ssrin cicc chtval 2nn eleqtri ppisval2 sumeq1d eqbrtrd ) ABIUAJZCUBKZCU
        EKZUFJZUFJZBUVSUFJZBUVTUFJZUFJZCUGKZLAUWBBBUFJZUWAUFJZUWEAUVRUWGUWAUFAB
        UCMZUVRUWGNABABUDMZBOMZDBUHPZUIZBUJPUKAUWIUWIUVSUCMZUVTUCMUWHUWENUWMUWM
        AUVSACOMZUVSULMUVSOMZAUWOICQRZACIUMUNJMZUWOUWQUOZFIOMZUWRUWSUPUQICURUSU
        TZVAZCVBUVSVCVDZUIZAUVTACUDMZUVTOMZAUWOVECLRUXEUXBAVEICVEOMAVFVGUWTAUQV
        GZUXBVEILRAVKVGAUWOUWQUXAVHZVICVLVJZCVMPZUIBBUVSUVTVNVOVPAUWEUVSCBVQJZU
        BKZVRJZUWDUFJZUWFAUWCOMZUWDOMZUWEOMAUWKUWPUXOUWLUXCBUVSVSSZAUWKUXFUXPUW
        LUXJBUVTVSSZUWCUWDVSSAUXMOMZUXPUXNOMAUWPUXLOMZUXSUXCAUXKOMZUXLULMUXTAUX
        KUDMZUYAAUXEUWKUYBUXIUWLCBVTSZUXKUHPZUXKVBUXLVCVDZUVSUXLWASZUXRUXMUWDVS
        SZAUWOUWFOMUXBCWBPZAUWCUXMLRZUWEUXNLRZAUWCUVSUXKVRJZUXMUXQAUWPUYAUYKOMZ
        UXCUYDUVSUXKWASZUYFAUWCUYKLRZBUYKUVSWCJZLRZABTUXKUVSWCJZVRJZUYOLAUYQTBV
        RJLRZBUYRLRZGAUYQOMZTOMZUWKUYSUYTUPAUYAUVSUDMZVUAUYDAUVSWMMZVUCAUWSVUDU
        XACWDPUVSWEPZUXKUVSWFSVUBAWGVGZUWLUYQTBWHWIWJAUYOUVSUVSWCJZUYQVRJZUYRAU
        WNUXKUCMUWNUVSVEWKUOZUYOVUHNUXDAUXKUYDUIAVUCVUIVUEUVSWLPZUVSUXKUVSWNWIA
        VUGTUYQVRAVUIVUGTNVUJUVSWOPUKVPWPAUWKUYLUWPVEUVSLRUOZUYNUYPUPUWLUYMAVUC
        VUKVUEUVSVLUTBUYKUVSWQWIWRAUXLUXKLRZUYKUXMLRZAUYBVULUYCUXKWSPAUXTUYAUWP
        VULVUMUPUYEUYDUXCUXLUXKUVSWTWIWJXAAUXOUXSUXPVEUWDLRUOZUYIUYJUPUXQUYFAUW
        DUDMZVUNAUWJUVTUDMZVUODAUWOTCLRZVUPUXBATICVUFUXGUXBTILRAXCVGUXHVIZCXBSB
        UVTXDSUWDVLUTUWCUXMUWDXEWIWJAUXNUXKXFKZTXGJZCXFKZXHJZXIXJZHXKZUEKZHXLZU
        WFUYGAVVCVVEHAVVBXMMVVCVVBXNVVCXMMZAVUTVVAXOVVBXIXPZVVBVVCYGXQZAVVDVVCM
        ZUOZVVDUDMZVVEOMZVVKVVDXIMZVVLVVKVVCXIVVDVVBXIXRAVVJXSZXTVVNVVDWMMZVVLV
        VDYAVVDWEPPZVVDVMPZYBUYHAVVCUWDHXLZUXNVVFQAVVSVVCYCKZUWDUFJZUXNAVVGUWDU
        CMVVSVWANVVIAUWDUXRUIVVCUWDHYDSAUXMVVTUWDUFAVVAUBKZVUSUBKZVRJZUXMVVTAVW
        BUVSVWCUXLVRAUWOVWBUVSNUXBCYEPAUYAVWCUXLNUYDUXKYEPYFAVVAVUSYHKMZVWDVVTN
        AVUSYIMZVVAYIMZVUSVVAQRZVWEAUYAVWFUYDUXKYJPAUWOVWGUXBCYJPZAUYAUWOUXKCQR
        VWHUYDUXBAUXKCTVQJZCQAUWOTCQRZUWKVUBBTQRZUXKVWJQRUXBAVUQVWKVURAVUBUWOVU
        QVWKYKWGUXBTCYLYMYNUWLVUFABTLRZVWLEAUWKVUBVWMVWLYKUWLWGBTYLXQYNCBTYOUUA
        ACUCMZVWJCNACUXBUIZCUUJPUUBUXKCUUCWIVUSVVAUUDUUEVUSVVAUUFPUUGUKUUHAVVCU
        WDVVEHVVIAUXPVVJUXRYPZVVRVVKUWDVVEQRZUWDYQKZVVEYQKZQRZVVKUXKVVDVWRVWSQV
        VKUXKVUTVVDAUYAVVJUYDYPAVUTOMZVVJAVUSOMZVXAAUYAVXBUYDUXKUUKPVUSUUIPZYPV
        VKVVLVVDOMZVVQVVDUHPAUXKVUTQRZVVJAUXKVUTLRZVXEAUYAVXFUYDUXKUULPAUYAVXAV
        XFVXEYKUYDVXCUXKVUTYLSYNYPVVKVVDVVBMVUTVVDQRVVKVVCVVBVVDVVHVVOXTVVDVUTV
        VAUUMPYRAVWRUXKNVVJAUXKVWRAVWNCVEWKZUWIUXKVWRNVWOAUXEVXGUXICUUNPUWMCBUU
        TWIUUOYPVVKVVLVWSVVDNVVQVVDUUPPUUQVVKUXPVVMVWQVWTUPVWPVVRUWDVVEUVASWRUU
        RUUSAVVFTVVAXHJZXIXJZVVEHXLZUWFQAVXIVVEVVCHAVXHXMMVXIVXHXNVXIXMMATVVAXO
        VXHXIXPVXHVXIYGXQAVVDVXIMZUOZVVEUDMZVVMVXLVXDTVVDLRZVXMVXLVVPVXDVXLVVPV
        XNVXLVVDIYHKMZVVPVXNUOVXLVVNVXOVXLVXIXIVVDVXHXIXRAVXKXSXTVVDUVBPVVDUVCU
        TZVAVVDUVDPVXLVVPVXNVXPVHVVDXBSZVVEUHPVXLVXMVEVVEQRVXQVVEYSPAVVBVXHXNZV
        VCVXIXNAVUTTYHKZMVWGVXRAVUTWMVXSAVUSULMZVUTWMMAUYAVEUXKQRZVXTUYDAUYBVYA
        UYCUXKYSPUXKUVESVUSUVFPYTUVGVWIVUTTVVAYIUVHSVVBVXHXIUVJPUVIAUWFVECUVKJX
        IXJZVVEHXLZVXJAUWOUWFVYCNUXBCHUVLPAVYBVXIVVEHAUWOIVXSMVYBVXINUXBIWMVXSU
        VMYTUVNCTUVOXQUVPVPWPYRVIUVQ $.
        $( [22-Sep-2014] $)
    $}

    $( Lemma for ~ chtppilim . $)
    chtppilimlem2 $p |- ( ph -> E. z e. RR A. x e. ( 2 [,) +oo ) ( z <_ x ->
      ( ( A ^ 2 ) x. ( ( ppi ` x ) x. ( log ` x ) ) ) < ( theta ` x ) ) ) $=
      ( wbr co cdiv cc0 c1 clt c2 cr crp cc wcel wa syl syl2anc cv cle ccxp cfv
      cppi cmin cabs cpnf cico wral wrex cexp clog cmul ccht simpr 2re elicopnf
      wi wb ax-mp sylib simpld 0re a1i 2pos simprd ltletrd elrp sylanbrc adantr
      rpre rpcxpcl cn ppinncl nnrp rpdivcl 0cn 1re difrp sylancl mpbid cmpt cof
      crli cvv ovex 1lt2 rplogcl eqidd offval2 wceq rpcnne0 div23 syl3anc recnd
      rpcn dmdcan mulcom ax-1cn cxpsub nncan sylancr oveq2d cxp1 oveq1d 3eqtr3d
      wne 3eqtr4d eqtr3d mpteq2dva eqtrd co1 chebbnd1 ssrdv cxploglim o1rlimmul
      ex rlimres2 eqbrtrrd rlimi subid1 fveq2d rpge0 absid breq1d simprl simprr
      chtppilimlem1 expr sylbid imim2d ralimdva reximdv mpd ) ACUABUAZUBGZYPDUC
      HZYPUEUDZIHZJUFHZUGUDZKDUFHZLGZUSZBMUHUIHZUJZCNUKYQDMULHYSYPUMUDZUNHUNHYP
      UOUDLGZUSZBUUFUJZCNUKACBUUFYTJUUCOPAYPUUFQZRZYROQZYSOQZYTOQZUUMYPOQZDNQZU
      UNUUMYPNQZJYPLGUUQUUMUUSMYPUBGZUUMUULUUSUUTRZAUULUPMNQZUULUVAUTUQMYPURVAV
      BZVCZUUMJMYPJNQUUMVDVEUVBUUMUQVEZUVDJMLGUUMVFVEUUMUUSUUTUVCVGZVHYPVIVJZAU
      URUULADOQZUUREDVLSZVKZYPDVMTUUMYSVNQZUUOUUMUVAUVKUVCYPVOSYSVPSZYRYSVQTZJP
      QAVRVEZADKLGZUUCOQZFAUURKNQZUVOUVPUTUVIVSDKVTWAWBZABUUFYPUUHIHZYSIHZWCZBU
      UFUUHYPUUCUCHZIHZWCZUNWDHZBUUFYTWCZJWEAUWEBUUFUVTUWCUNHZWCUWFABUUFUVTUWCU
      NUWAUWDWFOOUUFWFQAMUHUIWGVEUUMUVSOQZUUOUVTOQUUMUUQUUHOQZUWHUVGUUMUUSKYPLG
      UWIUVDUUMKMYPUVQUUMVSVEUVEUVDKMLGUUMWHVEUVFVHYPWITZYPUUHVQTZUVLUVSYSVQTUU
      MUWIUWBOQZUWCOQZUWJUUMUUQUUCNQZUWLUVGUUMUVPUWNAUVPUULUVRVKUUCVLSZYPUUCVMT
      ZUUHUWBVQTZAUWAWJAUWDWJWKABUUFUWGYTUUMUVSUWCUNHZYSIHZUWGYTUUMUVSPQZUWCPQZ
      YSPQYSJXHRZUWSUWGWLUUMUWHUWTUWKUVSWQSZUUMUWMUXAUWQUWCWQSZUUMUUOUXBUVLYSWM
      SUVSUWCYSWNWOUUMUWRYRYSIUUMUWCUVSUNHZYPUWBIHZUWRYRUUMUUHPQUUHJXHRZUWBPQUW
      BJXHRZYPPQZUXEUXFWLUUMUWIUXGUWJUUHWMSUUMUWLUXHUWPUWBWMSUUMYPUVDWPZUUHUWBY
      PWRWOUUMUWTUXAUWRUXEWLUXCUXDUVSUWCWSTUUMYPKUUCUFHZUCHZYPKUCHZUWBIHZYRUXFU
      UMUXIYPJXHRZKPQZUUCPQUXLUXNWLUUMUUQUXOUVGYPWMSUXPUUMWTVEUUMUUCUWOWPYPKUUC
      XAWOUUMUXKDYPUCUUMUXPDPQUXKDWLWTUUMDUVJWPKDXBXCXDUUMUXMYPUWBIUUMUXIUXMYPW
      LUXJYPXESXFXGXIXFXJXKXLAUWAXMQUWDJWEGUWEJWEGBXNABUUFOUWCJPABUUFOAUULUUQUV
      GXRXOUVNAUVPBOUWCWCJWEGUVRUUCBXPSXSUWAUWDXQXCXTYAAUUGUUKCNAUUEUUJBUUFUUMU
      UDUUIYQUUMUUDYTUUCLGZUUIUUMUUBYTUUCLUUMUUBYTUGUDZYTUUMUUAYTUGUUMYTPQUUAYT
      WLUUMYTUUMUUPYTNQZUVMYTVLSZWPYTYBSYCUUMUXSJYTUBGZUXRYTWLUXTUUMUUPUYAUVMYT
      YDSYTYETXLYFAUULUXQUUIAUULUXQRZRDYPAUVHUYBEVKAUVOUYBFVKAUULUXQYGAUULUXQYH
      YIYJYKYLYMYNYO $.
      $( [22-Sep-2014] $)
  $}

  ${
    $d x y z $.
    $( The ` theta ` function is asymptotic to ` ppi ( x ) log ( x ) ` , so it
       is sufficient to prove ` theta ( x ) / x ~~>r 1 ` to establish the
       PNT. $)
    chtppilim $p |- ( x e. ( 2 [,) +oo ) |->
      ( ( theta ` x ) / ( ( ppi ` x ) x. ( log ` x ) ) ) ) ~~>r 1 $=
      ( vz c2 co cfv cmul c1 wbr wtru cle clt cr crp wcel cc0 1re a1i wb adantl
      syl vy cpnf cico cv ccht cppi clog cdiv cmpt crli cmin cabs wral wrex cif
      wi csqr cexp rehalfcl ax-mp rpre resubcl sylancr ifcl 0re halfgt0 sylancl
      max2 ltletrd elrp sylanbrc rpsqrcl halflt1 ltsubrp mpan breq1 ifboth lt01
      rpge0 ltleii sqrlt syl22anc mpbid sqr1 syl6breq chtppilimlem2 adantr max1
      wa elicopnf simplbi chtcl ppinncl sylbi nnrp 1lt2 simprbi rplogcl syl2anc
      2re cn rpmulcl rerpdivcl lelttr syl3anc mpand wceq cc recnd oveq1d breq1d
      sqrth sylib ltmuldiv bitrd 2pos chtleppi rpcn breqtrrd ledivmul abssuble0
      mulid1 mpbird ltsub23 3imtr4d imim2d ralimdva reximdv mpd rgen wss ax-1cn
      ssriv rlim2 mpbiri trud ) ACUBUCDZAUDZUEEZYRUFEZYRUGEZFDZUHDZUIGUJHZIUUDB
      UDYRJHZUUCGUKDULEZUAUDZKHZUPZAYQUMZBLUNZUAMUMUUKUAMUUGMNZUUEGUUGUKDZGCUHD
      ZJHZUUNUUMUOZUQEZCURDZUUBFDZYSKHZUPZAYQUMZBLUNUUKUULABUUQUULUUPMNZUUQMNUU
      LUUPLNZOUUPKHUVCUULUUNLNZUUMLNZUVDGLNZUVEPGUSUTZUULUVGUUGLNZUVFPUUGVAZGUU
      GVBVCZUUOUUNUUMLVDVCZUULOUUNUUPOLNZUULVEQUVEUULUVHQUVLOUUNKHUULVFQUULUVFU
      VEUUNUUPJHUVKUVHUUMUUNVHVGVIUUPVJVKZUUPVLTUULUUQGUQEZGKUULUUPGKHZUUQUVOKH
      ZUULUUNGKHZUUMGKHZUVPVMUVGUULUVSPGUUGVNVOUUOUVRUVSUVPUUNUUMUUNUUPGKVPUUMU
      UPGKVPVQVCUULUVDOUUPJHZUVGOGJHZUVPUVQRUVLUULUVCUVTUVNUUPVSTUVGUULPQUWAUUL
      OGVEPVRVTQUUPGWAWBWCWDWEWFUULUVBUUJBLUULUVAUUIAYQUULYRYQNZWIZUUTUUHUUEUWC
      UUPUUCKHZUUMUUCKHZUUTUUHUWCUUMUUPJHZUWDUWEUWCUVFUVEUWFUULUVFUWBUVKWGZUVHU
      UMUUNWHVGUWCUVFUVDUUCLNZUWFUWDWIUWEUPUWGUULUVDUWBUVLWGZUWBUWHUULUWBYSLNZU
      UBMNZUWHUWBYRLNZUWJUWBUWLCYRJHZCLNZUWBUWLUWMWIZRWTCYRWJUTZWKZYRWLTZUWBYTM
      NZUUAMNZUWKUWBYTXANZUWSUWBUWOUXAUWPYRWMWNYTWOTUWBUWLGYRKHUWTUWQUWBGCYRUVG
      UWBPQZUWNUWBWTQZUWQGCKHUWBWPQUWBUWLUWMUWPWQZVIYRWRWSYTUUAXBWSZYSUUBXCWSZS
      ZUUMUUPUUCXDXEXFUWCUUTUUPUUBFDZYSKHZUWDUWCUUSUXHYSKUWCUURUUPUUBFUULUURUUP
      XGZUWBUULUUPXHNUXJUULUUPUVLXIUUPXLTWGXJXKUWCUVDUWJUUBLNOUUBKHWIZUXIUWDRUW
      IUWBUWJUULUWRSUWBUXKUULUWBUWKUXKUXEUUBVJXMZSUUPYSUUBXNXEXOUWCUUHGUUCUKDZU
      UGKHZUWEUWBUUHUXNRUULUWBUUFUXMUUGKUWBUWHUVGUUCGJHZUUFUXMXGUXFUXBUWBUXOYSU
      UBGFDZJHZUWBYSUUBUXPJUWBYRMNZYSUUBJHUWBUWLOYRKHUXRUWQUWBOCYRUVMUWBVEQUXCU
      WQOCKHUWBXPQUXDVIYRVJVKYRXQTUWBUUBXHNZUXPUUBXGUWBUWKUXSUXEUUBXRTUUBYBTXSU
      WBUWJUVGUXKUXOUXQRUWRUXBUXLYSGUUBXTXEYCUUCGYAXEXKSUWCUVGUWHUVIUXNUWERUVGU
      WCPQUXGUULUVIUWBUVJWGGUUCUUGYDXEXOYEYFYGYHYIYJIUABAYQUUCGUWBUUCXHNIUWBUUC
      UXFXISYQLYKIAYQLUWQYMQGXHNIYLQYNYOYP $.
      $( [22-Sep-2014] $)
  $}

  $( The ` theta ` function is upper bounded by a linear term.  Corollary of
     ~ chtub . $)
  chto1ub $p |- ( x e. RR+ |-> ( ( theta ` x ) / x ) ) e. O(1) $=
    ( crp cfv co wcel wtru c3 c2 cmul cr a1i cc 3re 2re cle wbr cc0 clt adantr
    wa cv ccht cdiv cmpt co1 clog wss rpssre rpre chtcl rerpdivcl mpancom recnd
    adantl 2pos elrpii relogcl ax-mp remulcli cabs wceq chtge0 rpregt0 syl21anc
    syl divge0 absid syl2anc cmin remulcl sylancr resubcl sylancl simpr ltletrd
    2lt3 chtub 3pos ltsubrp wb 1lt2 rplogcl mp2an elrp mpbi syl3anc mpbid lttrd
    c1 ltmul2 recni 2cn mulass breqtrrd ltdivmul2 mpbird ltle mpd eqbrtrd elo1d
    wi trud ) ABAUAZUBCZXCUCDZUDUEEFABXEGHUFCZHIDZBJUGFUHKXCBEZXELEFXHXEXDJEZXH
    XEJEZXHXCJEZXIXCUIZXCUJVEZXDXCUKULZUMUNGJEZFMKXGJEZFXFHHBEXFJEZHNUOUPHUQURZ
    NUSZKXHGXCOPZTZXEUTCZXGOPFYAYBXEXGOXHYBXEVAZXTXHXJQXEOPZYCXNXHXIQXDOPZXKQXC
    RPTZYDXMXHXKYEXLXCVBVEXCVCZXDXCVFVDXEVGVHSYAXEXGRPZXEXGOPZYAYHXDXGXCIDZRPZY
    AXDXFHXCIDZIDZYJRYAXDXFYLGVIDZIDZYMXHXIXTXMSZYAXQYNJEZYOJEXRYAYLJEZXOYQYAHJ
    EZXKYRNXHXKXTXLSZHXCVJVKZMYLGVLVMZXFYNVJVKYAXQYRYMJEXRUUAXFYLVJVKYAXKHXCRPX
    DYORPYTYAHGXCYSYANKXOYAMKYTHGRPYAVPKXHXTVNVOXCVQVHYAYNYLRPZYOYMRPZYAYRGBEUU
    CUUAGMVRUPYLGVSVMYAYQYRXQQXFRPTZUUCUUDVTUUBUUAUUEYAXFBEZUUEYSWIHRPUUFNWAHWB
    WCXFWDWEKYNYLXFWJWFWGWHYAXFLEZHLEZXCLEZYJYMVAUUGYAXFXRWKKUUHYAWLKXHUUIXTXHX
    CXLUMSXFHXCWMWFWNYAXIXPYFYHYKVTYPXPYAXSKXHYFXTYGSXDXGXCWOWFWPYAXJXPYHYIXAXH
    XJXTXNSXSXEXGWQVMWRWSUNWTXB $.
    $( [22-Sep-2014] $)

  $( The Chebyshev bound, part 2:  The function ` ppi ( x ) ` is eventually
     upper bounded by a positive constant times ` x / log ( x ) ` .
     Alternatively stated, the function ` ppi ( x ) / ( x / log ( x ) ) ` is
     eventually bounded.  (Contributed by Mario Carneiro, 22-Sep-2014.) $)
  chebbnd2 $p |- ( x e. ( 2 [,) +oo ) |->
    ( ( ppi ` x ) / ( x / ( log ` x ) ) ) ) e. O(1) $=
    ( c2 co cdiv cmpt co1 wcel wtru c1 cvv a1i wa cc cc0 wne crp cr wbr syl2anc
    syl cpnf cico cv cppi cfv clog ccht cmul cof ovex eqidd wceq simpr elicopnf
    cle 2re ax-mp sylib chtrpcl rpcnne0 ppinncl nnrp clt simpld 1re 1lt2 simprd
    wb cn ltletrd rplogcl rpmulcl recdiv mpteq2dva offval2 0re 2pos elrp dmdcan
    sylanbrc syl3anc rpcn divdiv2 eqtr4d eqtrd cres wss ex ssrdv resmpt chto1ub
    o1res syl6eqelr crli ax-1cn rpdivcl csn cxp fconstmpt cxr icossre rlimconst
    pnfxr mp2an eqbrtrri chtppilim ax-1ne0 rpne0 rlimdiv rlimo1 o1mul eqeltrrd
    trud ) ABUAUBCZAUCZUDUEZXOXOUFUEZDCDCZEZFGHAXNXOUGUEZXODCZEZAXNIXTXPXQUHCZD
    CZDCZEZUHUICZXSFHYGAXNYAYCXTDCZUHCZEXSHAXNYAYHUHYBYFJJJXNJGHBUAUBUJKYAJGHXO
    XNGZLZXTXODUJKYHJGYKYCXTDUJKHYBUKHAXNYEYHYKXTMGXTNOLZYCMGZYCNOZLZYEYHULYKXT
    PGZYLYKXOQGZBXOUORZLZYPYKYJYSHYJUMBQGZYJYSVHUPBXOUNUQURZXOUSTZXTUTTZYKYCPGZ
    YOYKXPPGZXQPGZUUDYKXPVIGZUUEYKYSUUGUUAXOVATXPVBTZYKYQIXOVCRUUFYKYQYRUUAVDZY
    KIBXOIQGYKVEKYTYKUPKZUUIIBVCRYKVFKYKYQYRUUAVGZVJXOVKSZXPXQVLSZYCUTTZXTYCVMS
    VNVOHAXNYIXRYKYIYCXODCZXRYKYLXOMGXONOLZYMYIUUOULUUCYKXOPGZUUPYKYQNXOVCRUUQU
    UIYKNBXONQGYKVPKUUJUUINBVCRYKVQKUUKVJXOVRVTZXOUTTZYKYMYNUUNVDXTXOYCVSWAYKXP
    MGZUUPXQMGXQNOLZXRUUOULYKUUEUUTUUHXPWBTUUSYKUUFUVAUULXQUTTXPXOXQWCWAWDVNWEH
    YBFGYFFGZYGFGHYBAPYAEZXNWFZFHXNPWGUVDYBULHAXNPHYJUUQUURWHWIAPXNYAWJTUVCFGUV
    DFGAWKXNUVCWLUQWMHYFIIDCZWNRUVBHAXNIYDIIMIMGZHWOKZUVGUVFYKWOKYKYDPGZYDMGYKY
    PUUDUVHUUBUUMXTYCWPSZYDWBTAXNIEZIWNRHXNIWQWRZUVJIWNAXNIWSXNQWGZUVFUVKIWNRYT
    UAWTGUVLUPXCBUAXAXDWOXNIXBXDXEKAXNYDEIWNRHAXFKINOHXGKYKUVHYDNOUVIYDXHTXIUVE
    YFXJTYBYFXKSXLXM $.
    $( [22-Sep-2014] $)

  $(
  @{
    selberg.1 @e |- S = sum_ p e. ( ( 0 [,] x ) i^i Prime )
      ( ( log ` p ) x. ( theta ` ( x / p ) ) ) @.
    selberg.2 @e |- T = ( ( ( ( theta ` x ) x. ( log ` x ) ) + S )
      - ( ( 2 x. x ) x. ( log ` x ) ) ) @.
    @( Selberg's asymptotic formula involving the Chebyshev ` theta ` function.
       @)
    selberg @p |- ( x e. RR+ |-> ( T / x ) ) e. O(1) @=
      ? @.
  @}

  @{
    @d m n x @.
    @( The Prime Number Theorem: the number of prime numbers less than ` x `
       tends asymptotically to ` x / log ( x ) ` as ` x ` goes to infinity. @)
    pnt @p |- ( x e. ( 1 (,) +oo ) |->
      ( ( ppi ` x ) / ( x / ( log ` x ) ) ) ) ~~>r 1 @=
      ? @.
  @}
  $)

