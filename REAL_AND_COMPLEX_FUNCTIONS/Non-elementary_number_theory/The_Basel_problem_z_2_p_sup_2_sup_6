$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              The Basel problem (&zeta;(2) = &pi;<sup>2</sup>/6)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k m t A $.  $d j k m n t x y M $.  $d j k m n t x N $.  $d k n x P $.
    $d k m x y T $.
    basel.n $e |- N = ( ( 2 x. M ) + 1 ) $.
    $( Lemma for ~ basel .  Closure of the sequence of roots. $)
    basellem1 $p |- ( ( M e. NN /\ K e. ( 1 ... M ) ) ->
      ( ( K x. pi ) / N ) e. ( 0 (,) ( pi / 2 ) ) ) $=
      ( cn wcel c1 co cpi cmul cr cc0 clt wbr c2 crp syl pire sylancl nnre cdiv
      cfz cioo elfznn nnrp pipos elrpii rpmulcl caddc 2nn nnmulcl mpan peano2nn
      wa syl5eqel rpdivcl syl2anr rpre rpgt0 adantl adantr syl5eqelr wceq recnd
      cle cc 2cn mulcom elfzle2 wb 2re 2pos pm3.2i lemul2 syl3anc mpbid eqbrtrd
      a1i ltp1 lelttrd syl6breqr remulcl nngt0 ltdiv2 syl33anc recni 2ne0 nnne0
      wne divcan5 syl112anc breqtrd cxr w3a 0xr rehalfcl rexr mp2b elioo2 mp2an
      syl3anbrc ) BEFZAGBUBHFZUNZAIJHZCUAHZKFZLXFMNZXFIOUAHZMNZXFLXIUCHFZXDXFPF
      ZXGXCXEPFZCPFZXLXBXCAPFZIPFXMXCAEFZXOABUDZAUEQIRUFUGAIUHSZXBCEFZXNXBCOBJH
      ZGUIHZEDXBXTEFZYAEFOEFZXBYBUJOBUKULZXTUMQUOZCUEQXECUPUQZXFURQXDXLXHYFXFUS
      QXDXFXEAOJHZUAHZXIMXDYGCMNZXFYHMNZXDYGYACMXDYGXTYAXDYGEFZYGKFZXDXPYCYKXCX
      PXBXQUTZUJAOUKSZYGTQZXDYBXTKFZXBYBXCYDVAXTTQZXDYACKDXDXSCKFZXBXSXCYEVAZCT
      QZVBXDYGOAJHZXTVEXDAVFFZOVFFZYGUUAVCXDAXDXPAKFZYMATQZVDZVGAOVHSXDABVENZUU
      AXTVENZXCUUGXBAGBVIUTXDUUDBKFZOKFZLOMNZUNZUUGUUHVJUUEXBUUIXCBTVAUULXDUUJU
      UKVKVLVMVRABOVNVOVPVQXDYPXTYAMNYQXTVSQVTDWAXDYLYRXEKFZLYGMNZLCMNZLXEMNZYI
      YJVJYOYTXDUUDIKFZUUMUUERAIWBSXDYKUUNYNYGWCQXDXSUUOYSCWCQXDXMUUPXCXMXBXRUT
      XEUSQYGCXEWDWEVPXDIVFFZUUCOLWIZUNZUUBALWIZYHXIVCUURXDIRWFVRUUTXDUUCUUSVGW
      GVMVRUUFXDXPUVAYMAWHQIOAWJWKWLLWMFXIWMFZXKXGXHXJWNVJWOUUQXIKFUVBRIWPXIWQW
      RLXIXFWSWTXA $.
      $( [30-Jul-2014] $)

    basel.p $e |- P = ( t e. CC |-> sum_ j e. ( 0 ... M )
      ( ( ( N _C ( 2 x. j ) ) x. ( -u 1 ^ ( M - j ) ) ) x. ( t ^ j ) ) ) $.
    $( Lemma for ~ basel .  Show that ` P ` is a polynomial of degree ` M ` ,
       and compute its coefficient function. $)
    basellem2 $p |- ( M e. NN -> ( P e. ( Poly ` CC ) /\ ( deg ` P ) = M /\
      ( coeff ` P ) = ( n e. NN0 |->
        ( ( N _C ( 2 x. n ) ) x. ( -u 1 ^ ( M - n ) ) ) ) ) ) $=
      ( wcel cc wceq cn0 c2 cmul co c1 cc0 syl cz wbr cn cply cfv cdgr ccoe cbc
      cv cneg cmin cexp cmpt cfz csu wss ssid a1i nnnn0 wa elfznn0 oveq2 oveq2d
      oveq12d eqid ovex fvmpt adantl wf caddc nnmulcl mpan peano2nn syl5eqel 2z
      2nn nn0z zmulcl sylancr syl2an nn0cn nnz zsubcl wne ax-1cn negcli ax-1ne0
      bccl negne0i expclz mp3an12 mulcl syl2anc fmptd ffvelrn eqeltrrd cuz cima
      elplyd csn cle wi wral wn clt cr wb nnre nn0re ltnle ad2antlr wo ad2antrr
      2timesi oveq2i 2cn nncn adddi syl3anc oveq1i addass syl5eq 3eqtr4a biimpa
      zltp1le peano2re 2re 2pos pm3.2i lemul2 mpbid eqbrtrrd mpbird olcd bcval4
      oveq1d adantr mul02 3eqtrd ex sylbird 3syl necon1ad plyco0 sumeq2i eqtr4i
      ralrimiva mpteq2i subid exp0 ax-mp syl6eq lep1 nn0uz syl6eleq elfz5 bccl2
      syl6breqr mulid1 nnne0 eqnetrd dgreq coeeq 3jca ) EUAIZBJUBUCZIBUDUCEKBUE
      UCDLFMDUGZNOZUFOZPUHZEUVEUIOZUJOZNOZUKZKUVCBAJQEULOZFMCUGZNOZUFOZUVHEUVNU
      IOZUJOZNOZAUGUVNUJOZNOZCUMZUKZUVDHUVCAUVSJCEJJUNUVCJUOUPEUQZUVCUVNUVMIZUR
      UVNUVLUCZUVSJUWEUWFUVSKZUVCUWEUVNLIZUWGUVNEUSZDUVNUVKUVSLUVLUVEUVNKZUVGUV
      PUVJUVRNUWJUVFUVOFUFUVEUVNMNUTVAUWJUVIUVQUVHUJUVEUVNEUIUTVAVBUVLVCZUVPUVR
      NVDVEZRZVFUVCLJUVLVGZUWHUWFJIUWEUVCDLUVKJUVLUVCUVELIZURZUVGJIZUVJJIZUVKJI
      UWPUVGLIZUWQUVCFLIZUVFSIZUWSUWOUVCFUAIZUWTUVCFMENOZPVHOZUAGUVCUXCUAIZUXDU
      AIMUAIUVCUXEVNMEVIVJZUXCVKRVLZFUQRZUWOMSIZUVESIZUXAVMUVEVOZMUVEVPVQUVFFWF
      VRUVGVSRUWPUVISIZUWRUVCESIZUXJUXLUWOEVTZUXKEUVEWAVRUVHJIZUVHQWBZUXLUWRPWC
      WDZPWCWEWGZUVHUVIWHWIRUVGUVJWJWKUWKWLZUWILJUVNUVLWMVRWNWQVLZUVCAUVLJCBEUX
      TUWDUXSUVCUVLEPVHOZWOUCWPQWRKZUWFQWBUVNEWSTZWTZCLXAZUVCUYDCLUVCUWHURZUYCU
      WFQUYFUYCXBZEUVNXCTZUWFQKZUVCEXDIZUVNXDIZUYHUYGXEUWHEXFZUVNXGZEUVNXHVRUYF
      UYHUYIUYFUYHURZUWFUVSQUVRNOZQUWHUWGUVCUYHUWLXIUYNUVPQUVRNUYNUWTUVOSIZUVOQ
      XCTZFUVOXCTZXJUVPQKUVCUWTUWHUYHUXHXKUYNUXIUVNSIZUYPVMUWHUYSUVCUYHUVNVOZXI
      MUVNVPVQZUYNUYRUYQUYNUYRFPVHOZUVOWSTZUYNMUYANOZVUBUVOWSUYNUXCMPNOZVHOZUXC
      PPVHOZVHOZVUDVUBVUEVUGUXCVHPWCXLXMUYNMJIZEJIZPJIZVUDVUFKVUIUYNXNUPUVCVUJU
      WHUYHEXOZXKVUKUYNWCUPZMEPXPXQUYNVUBUXDPVHOZVUHFUXDPVHGXRUYNUXCJIZVUKVUKVU
      NVUHKUYNUXEVUOUVCUXEUWHUYHUXFXKUXCXORVUMVUMUXCPPXSXQXTYAUYNUYAUVNWSTZVUDU
      VOWSTZUYFUYHVUPUVCUXMUYSUYHVUPXEUWHUXNUYTEUVNYCVRYBUYNUYAXDIZUYKMXDIZQMXC
      TZURZVUPVUQXEUYNUYJVURUVCUYJUWHUYHUYLXKEYDRUWHUYKUVCUYHUYMXIVVAUYNVUSVUTY
      EYFYGUPUYAUVNMYHXQYIYJUYNFSIZUYPUYRVUCXEUVCVVBUWHUYHUVCUXBVVBUXGFVTRZXKVU
      AFUVOYCWKYKYLUVOFYMXQYNUYNUVRJIZUYOQKUYFVVDUYHUYFUVQSIZVVDUVCUXMUYSVVEUWH
      UXNUYTEUVNWAVRUXOUXPVVEVVDUXQUXRUVHUVQWHWIRYOUVRYPRYQYRYSUUAUUEUVCELIZUWN
      UYBUYEXEUWDUXSUVLCEUUBWKYKZBAJUVMUWFUVTNOZCUMZUKZKUVCBUWCVVJHAJVVIUWBUVMV
      VHUWACUWEUWFUVSUVTNUWMYNUUCUUFUUDUPZUVCEUVLUCZFUXCUFOZQUVCVVLVVMUVHEEUIOZ
      UJOZNOZVVMPNOZVVMUVCVVFVVLVVPKUWDDEUVKVVPLUVLUVEEKZUVGVVMUVJVVONVVRUVFUXC
      FUFUVEEMNUTVAVVRUVIVVNUVHUJUVEEEUIUTVAVBUWKVVMVVONVDVERUVCVVOPVVMNUVCVVOU
      VHQUJOZPUVCVVNQUVHUJUVCVUJVVNQKVULEUUGRVAUXOVVSPKUXQUVHUUHUUIUUJVAUVCVVMU
      AIZVVMJIVVQVVMKUVCUXBUXCQFULOIZVVTUXGUVCVWAUXCFWSTZUVCUXCUXDFWSUVCUXEUXCX
      DIUXCUXDWSTUXFUXCXFUXCUUKYTGUUPUVCUXCQWOUCZIVVBVWAVWBXEUVCUXCLVWCUVCUXEUX
      CLIUXFUXCUQRUULUUMVVCUXCQFUUNWKYKUXCFUAUUOWKZVVMXOVVMUUQYTYQUVCVVTVVMQWBV
      WDVVMUURRUUSUUTUVCAUVLJCBEUXTUWDUXSVVGVVKUVAUVB $.
      $( [30-Jul-2014] $)

    $( Lemma for ~ basel .  Using the binomial theorem and de Moivre's formula,
       we have the identity ` _e ^ _i N x / ( sin x ) ^ n = `
       ` sum_ m e. ( 0 ... N ) ( N _C m ) ( _i ^ m ) ( cot x ) ^ ( N - m ) ` ,
       so taking imaginary parts yields ` sin ( N x ) / ( sin x ) ^ N = `
       ` sum_ j e. ( 0 ... M ) ( N _C 2 j ) ( -u 1 ) ^ ( M - j ) `
       ` ( cot x ) ^ ( -u 2 j ) = P ( ( cot x ) ^ 2 ) ` , where
       ` N = 2 M + 1 ` . $)
    basellem3 $p |- ( ( M e. NN /\ A e. ( 0 (,) ( pi / 2 ) ) ) ->
      ( P ` ( ( tan ` A ) ^ -u 2 ) ) =
        ( ( sin ` ( N x. A ) ) / ( ( sin ` A ) ^ N ) ) ) $=
      ( wcel cc0 c2 co cexp ci cmul cc wceq syl cr syl2anc vm vk cn cdiv wa cfz
      cv cbc c1 cfv cmin csu cim ccos csin cneg cn0 crp adantl rpcn 3syl ax-icn
      caddc a1i sylancr syl3anc wne recoscl recnd resincl mulcl sylanbrc divdir
      clt wbr rpne0 syl112anc oveq2d syl22anc oveq12d eqtrd oveq1d 3eqtr3d nnre
      cz nnz remulcl rpexpcl 3eqtrd eqtr3d fveq2d oveq2 wf1 adantr 2nn0 elfznn0
      fzfid cle nn0mulcl nn0re wb ad2antrr 2re 2pos pm3.2i mpbid mpbird ex nncn
      elfzelz ad2antrl simprr 2ne0 ax-mp ovex fvmpt sylan rpre fznn0sub reexpcl
      2cn sylan2 wo i2 oveq1i nn0ge0 divge0 mpanr12 nn0cn divcan2 eqeltrrd expr
      expmul ax-1cn syl5eq nncan nn0z sylancl mulass 3eqtr4d cioo tanrpcl simpl
      cpi rpreccl 2nn nnmulcl peano2nn syl5eqel nnnn0 binom elioore sincosq1sgn
      ctan addcl simpld elrp expdiv simprd gt0ne0 tanval recdiv eqtr2d demoivre
      syl121anc divcan4 divass cmpt crn wf1o copab elfzle2 lep1 syl6breqr letrd
      lemul2 cuz nn0uz syl6eleq elfz5 fznn0sub2 zcn ssriv sseldi subcan dom2lem
      mulcan bitrd df-mpt f1eq1 sylibr f1f1orn eqid wf wss f1f frn sselda bccl2
      expcl syldan imcl fsumf1o cdif eldifi wn eldif zeo elnn0z mp3an12 mp3an23
      syl5eqr 1re renegcli pnpcan2 mulid1i eqtr2i oveq2i addass adddi peano2zdi
      3eqtr4a zre peano2cn divsubdir divcan3 zsubcl eqeltrd 0z adantrr peano2re
      df-2 elfz subge02 ltp1 lelttrd breqtrd ltdivmul zleltp1 mpbir2and wfn ffn
      fnfvelrn orim12d mpd orcomd ord impr sylan2b fsumss bccl 2z znegcl syl2an
      reim0 addid2 mul12 bccmpl expneg mulneg1 exprec addsub subdi eqtr4d expp1
      expmulz 3eqtr2d syl6eq mulcom 3eqtr2rd 0re crim sumeq2dv fsumim sumeq2sdv
      oveq1 sumex rerpdivcl ) EUCIZBJUUDKUDLZUUALIZUEZJFUFLZFUAUGZUHLZUIBUUNUJZ
      UDLZFVWDUKLZMLZNVWDMLZOLZOLZUAULZUMUJZFBOLZUNUJZBUOUJZFMLZUDLZNVWOUOUJZVW
      RUDLZOLZVCLZUMUJZVWFKUPZMLZCUJZVXAVWBVWMVXCUMVWBVWGNVCLZFMLZVWMVXCVWBVWGP
      IZNPIZFUQIZVXIVWMQVWBVWFURIZVWGURIZVXJVWAVXMVVSBUUBUSZVWFUUEZVWGUTVAVXKVW
      BVBVDZVWBFUCIZVXLVWBFKEOLZUIVCLZUCGVWBVXSUCIZVXTUCIVWBKUCIVVSVYAUUFVVSVWA
      UUCZKEUUGVEZVXSUUHRUUIZFUUJRZVWGNUAFUUKVFVWBVXIVWPNVWTOLZVCLZVWRUDLZVWSVY
      FVWRUDLZVCLZVXCVWBBUNUJZNVWQOLZVCLZVWQUDLZFMLZVYMFMLZVWRUDLZVXIVYHVWBVYMP
      IZVWQPIZVWQJVGZVXLVYOVYQQVWBVYKPIZVYLPIZVYRVWBVYKVWBBSIZVYKSIZVWAWUCVVSBJ
      VVTUULUSZBVHRZVIZVWBVXKVYSWUBVBVWBVWQVWBWUCVWQSIZWUEBVJRZVIZNVWQVKVEZVYKV
      YLUUOTWUJVWBVWQURIZVYTVWBWUHJVWQVNVOZWULWUIVWBWUMJVYKVNVOZVWAWUMWUNUEVVSB
      UUMUSZUUPVWQUUQVLZVWQVPRZVYEVYMVWQFUURUVEVWBVYNVXHFMVWBVYNVYKVWQUDLZVYLVW
      QUDLZVCLZVXHVWBWUAWUBVYSVYTVYNWUTQWUGWUKWUJWUQVYKVYLVWQVMVQVWBWURVWGWUSNV
      CVWBVWGUIVWQVYKUDLZUDLZWURVWBVWFWVAUIUDVWBBPIZVYKJVGZVWFWVAQVWBBWUEVIZVWB
      WUDWUNWVDWUFVWBWUMWUNWUOUUSVYKUUTTZBUVATVRVWBVYSVYTWUAWVDWVBWURQWUJWUQWUG
      WVFVWQVYKUVBVSUVCVWBVXKVYSVYTWUSNQVXQWUJWUQNVWQUVFVFVTWAWBVWBVYPVYGVWRUDV
      WBWVCFWEIZVYPVYGQWVEVWBVXRWVGVYDFWFRZBFUVDTWBWCVWBVWPPIVYFPIZVWRPIZVWRJVG
      ZVYHVYJQVWBVWPVWBVWOSIZVWPSIZVWBFSIZWUCWVLVWBVXRWVNVYDFWDRZWUEFBWGTZVWOVH
      RZVIVWBVXKVWTPIZWVIVBVWBVWTVWBWVLVWTSIZWVPVWOVJRZVIZNVWTVKVEVWBVWRURIZWVJ
      VWBWULWVGWWBWUPWVHVWQFWHTZVWRUTRZVWBWWBWVKWWCVWRVPRZVWPVYFVWRVMVQVWBVYIVX
      BVWSVCVWBVXKWVRWVJWVKVYIVXBQVXQWWAWWDWWENVWTVWRUVGVQVRWIWJWKVWBVWCVWLUMUJ
      ZUAULZJEUFLZFKDUGZOLZUHLZUIUPZEWWIUKLZMLZOLZVXFWWIMLZOLZDULZVWNVXGVWBUBWW
      HFKUBUGZOLZUKLZUVHZUVIZWWFUAULWWHFFWWJUKLZUHLZVWGFWXDUKLZMLZNWXDMLZOLZOLZ
      UMUJZDULWWGWWRVWBWXCWWFWWHWXKUADWXBWXDVWDWXDQZVWLWXJUMWXLVWEWXEVWKWXIOVWD
      WXDFUHWLWXLVWIWXGVWJWXHOWXLVWHWXFVWGMVWDWXDFUKWLVRVWDWXDNMWLVTVTWKVWBJEWQ
      VWBWWHVWCWXBWMZWWHWXCWXBUVJVWBWWHVWCWWSWWHIZVWDWXAQUEUBUAUVKZWMZWXMVWBUBU
      AWWHVWCWXAFKVWDOLZUKLZVWBWXNWXAVWCIZVWBWXNUEZWVGWWTVWCIZWXSVWBWVGWXNWVHWN
      ZWXTWYAWWTFWRVOZWXTWWTVXSFWXTWWTUQIZWWTSIWXTKUQIZWWSUQIZWYDWOWXNWYFVWBWWS
      EWPUSZKWWSWSVEZWWTWTRVWBVXSSIZWXNVWBVYAWYIVYCVXSWDRWNZVWBWVNWXNWVOWNWXTWW
      SEWRVOZWWTVXSWRVOZWXNWYKVWBWWSJEUVLUSWXTWWSSIZESIZKSIZJKVNVOZUEZWYKWYLXAW
      XTWYFWYMWYGWWSWTRVVSWYNVWAWXNEWDXBWYQWXTWYOWYPXCXDXEZVDWWSEKUVPVFXFWXTVXS
      VXTFWRWXTWYIVXSVXTWRVOWYJVXSUVMRGUVNUVOWXTWWTJUVQUJZIWVGWYAWYCXAWXTWWTUQW
      YSWYHUVRUVSWYBWWTJFUVTTXGWEWWTFUWATXHVWBWXNVWDWWHIZUEZWXAWXRQZWWSVWDQZXAV
      WBXUAUEZXUBWWTWXQQZXUCXUDFPIZWWTPIZWXQPIZXUBXUEXAVWBXUFXUAVWBVXRXUFVYDFXI
      RZWNXUDKPIZWWSPIZXUGYAWXNXUKVWBWYTWXNWWSWEIXUKWWSJEXJWWSUWBRZXKZKWWSVKVEX
      UDXUJVWDPIZXUHYAXUDWWHPVWDUBWWHPXULUWCVWBWXNWYTXLUWDZKVWDVKVEFWWTWXQUWEVF
      XUDXUKXUNXUJKJVGZUEZXUEXUCXAXUMXUOXUQXUDXUJXUPYAXMXEZVDWWSVWDKUWGVFUWHXHU
      WFWXBWXOQWXMWXPXAUBUAWWHWXAUWIWWHVWCWXBWXOUWJXNUWKZWWHVWCWXBUWLRWWIWWHIZW
      WIWXBUJWXDQVWBUBWWIWXAWXDWWHWXBWWSWWIQWWTWWJFUKWWSWWIKOWLVRWXBUWMZFWWJUKX
      OXPUSVWBVWDWXCIZUEZWWFXVCVWLPIZWWFSIVWBXVBVWDVWCIZXVDVWBWXCVWCVWDVWBWXMWW
      HVWCWXBUWNZWXCVWCUWOXUSWWHVWCWXBUWPZWWHVWCWXBUWQVAZUWRVWBXVEUEZVWEPIVWKPI
      ZXVDXVIVWEXVIVWEUCIZVWESIZVWBVXRXVEXVKVYDVWDFUCUWSXQVWEWDRZVIXVIVWIPIVWJP
      IZXVJXVIVWIXVIVWGSIZVWHUQIZVWISIZVWBXVOXVEVWBVXMVXNXVOVXOVXPVWGXRVAWNVWBV
      XRXVEXVPVYDUCVWDJFXSXQZVWGVWHXTTZVIXVIVXKVWDUQIZXVNVBXVEXVTVWBVWDFWPZUSNV
      WDUWTVEVWIVWJVKTVWEVWKVKTZUXAVWLUXBRVIZUXCVWBWXCVWCWWFUAXVHXWCVWBVWDVWCWX
      CUXDIZUEZVWLSIZWWFJQXWEXVLVWKSIZXWFXWDVWBXVEXVLVWDVWCWXCUXEZXVMYBXWEXVQVW
      JSIZXWGXWDVWBXVEXVQXWHXVSYBXWDVWBXVEXVBUXFZUEXWIVWDVWCWXCUXGVWBXVEXWJXWIX
      VIXVBXWIXVIXWIXVBXVIVWDKUDLZWEIZVWDUIVCLZKUDLZWEIZYCZXWIXVBYCXVIVWDWEIZXW
      PXVEXWQVWBVWDJFXJUSVWDUXHRXVIXWLXWIXWOXVBVWBXVEXWLXWIVWBXVEXWLUEUEZWWLXWK
      MLZVWJSXWRXWSNKMLZXWKMLZVWJXWTWWLXWKMYDYEXWRNKXWKOLZMLZXXAVWJXWRXWKUQIZXX
      CXXAQZXWRXWLJXWKWRVOZXXDVWBXVEXWLXLXWRXVTXXFXVEXVTVWBXWLXWAXKZXVTVWDSIZJV
      WDWRVOZXXFVWDWTZVWDYFZXXHXXIUEWYOWYPXXFXCXDVWDKYGYHTRXWKUXIVLZVXKWYEXXDXX
      EVBWONKXWKYMUXJRXWRXXBVWDNMXWRXVTXUNXXBVWDQZXXGVWDYIZXUNXUJXUPXXMYAXMVWDK
      YJUXKVAVRWJUXLXWRWWLSIZXXDXWSSIUIUXMUXNZXXLWWLXWKXTVEYKYLVWBXVEXWOXVBVWBX
      VEXWOUEZUEZVWHKUDLZWXBUJZVWDWXCXXRXXTFKXXSOLZUKLZFVWHUKLZVWDXXRXXSWWHIZXX
      TXYBQXXRXYDJXXSWRVOZXXSEWRVOZXXRXXSWEIZJWEIZEWEIZXYDXYEXYFUEXAXXRXXSEUIVC
      LZXWNUKLZWEXXRXXSKXYJOLZXWMUKLZKUDLZXYLKUDLZXWNUKLZXYKXXRVWHXYMKUDXXRFUIV
      CLZXWMUKLZVWHXYMXXRXUFXUNUIPIZXYRVWHQVWBXUFXXQXUIWNZXXRXVTXUNXVEXVTVWBXWO
      XWAXKZXXNRZXYSXXRYNVDZFVWDUIUXOVFXXRXYQXYLXWMUKXXRVXSUIUIVCLZVCLZVXSKUIOL
      ZVCLZXYQXYLYUDYUFVXSVCYUFKYUDKYAUXPUYLUXQUXRXXRXYQVXTUIVCLZYUEFVXTUIVCGYE
      XXRVXSPIZXYSXYSYUHYUEQVWBYUIXXQVWBVYAYUIVYCVXSXIZRWNYUCYUCVXSUIUIUXSVFYOX
      XRXUJEPIZXYSXYLYUGQXUJXXRYAVDZVVSYUKVWAXXQEXIZXBYUCKEUIUXTVFUYBZWBWJWBXXR
      XYLPIZXWMPIZXUQXYNXYPQXXRXUJXYJPIZYUOYAXXRXYJXXRXYJWEIZXYJSIZXXREVVSXYIVW
      AXXQEWFXBZUYAZXYJUYCRZVIZKXYJVKVEXXRXVTXUNYUPYUAXXNVWDUYDVAXUQXXRXURVDXYL
      XWMKUYEVFXXRXYOXYJXWNUKXXRYUQXUJXUPXYOXYJQYVCYULXUPXXRXMVDZXYJKUYFVFWBWIX
      XRYURXWOXYKWEIYVAVWBXVEXWOXLXYJXWNUYGTUYHZXYHXXRUYIVDYUTXXSJEUYMVFXXRXVPX
      YEVWBXVEXVPXWOXVRUYJZXVPVWHSIZJVWHWRVOZXYEVWHWTZVWHYFYVGYVHUEWYOWYPXYEXCX
      DVWHKYGYHTRXXRXYFXXSXYJVNVOZXXRYVJVWHXYLVNVOZXXRVWHXYQXYLVNXXRVWHFXYQXXRX
      VPYVGYVFYVIRZVWBWVNXXQWVOWNZXXRWVNXYQSIYVMFUYKRXXRXXIVWHFWRVOZXXRXVTXXIYU
      AXXKRXXRWVNXXHXXIYVNXAYVMXXRXVTXXHYUAXXJRFVWDUYNTXFXXRWVNFXYQVNVOYVMFUYOR
      UYPYUNUYQXXRYVGYUSWYQYVJYVKXAYVLYVBWYQXXRWYRVDVWHXYJKUYRVFXGXXRXYGXYIXYFY
      VJXAYVEYUTXXSEUYSTXGUYTZUBXXSWXAXYBWWHWXBWWSXXSQWWTXYAFUKWWSXXSKOWLVRXVAF
      XYAUKXOXPRXXRXYAVWHFUKXXRVWHPIZXUJXUPXYAVWHQXXRXVPYVPYVFVWHYIRYULYVDVWHKY
      JVFVRXXRXUFXUNXYCVWDQXYTYUBFVWDYPTWIXXRWXBWWHVUAZXYDXXTWXCIVWBYVQXXQVWBWX
      MXVFYVQXUSXVGWWHVWCWXBVUBVAWNYVOWWHXXSWXBVUCTYKYLVUDVUEVUFVUGVUHVUIVWIVWJ
      WGTVWEVWKWGTVWLVUORVWBJFWQZVUJVWBWWHWXKWWQDVWBXUTUEZJNWWQOLZVCLZUMUJZWXKW
      WQYVSYWAWXJUMYVSYWAYVTWWKNWWNWWPOLZOLZOLZWXJYVSYVTPIZYWAYVTQYVSVXKWWQPIYW
      FVBYVSWWQYVSWWOSIZWWPSIZWWQSIZYVSWWKSIZWWNSIZYWGYVSWWKUQIZYWJYVSVXLWWJWEI
      ZYWLVWBVXLXUTVYEWNZYVSWWJUQIZYWMYVSWYEWWIUQIZYWOWOXUTYWPVWBWWIEWPZUSZKWWI
      WSVEZWWJYQRZWWJFVUKTWWKWTRZYVSXXOWWMUQIZYWKXXPVWBVVSXUTYXBVYBUCWWIJEXSXQZ
      WWLWWMXTVEZWWKWWNWGTVWBVXFSIZYWPYWHXUTVWBVXFURIZYXEVWBVXMVXEWEIZYXFVXOKWE
      IYXGVULKVUMXNZVWFVXEWHYRZVXFXRRYWQVXFWWIXTVUNZWWOWWPWGTZVINWWQVKVEYVTVUPR
      YVSYVTNWWKYWCOLZOLZYWEYVSWWQYXLNOYVSWWKPIZWWNPIZWWPPIZWWQYXLQYVSWWKYXAVIZ
      YVSWWNYXDVIZYVSWWPYXJVIZWWKWWNWWPYSVFVRYVSVXKYXNYWCPIZYXMYWEQVXKYVSVBVDZY
      XQYVSYXOYXPYXTYXRYXSWWNWWPVKTNWWKYWCVUQVFWAYVSWWKWXEYWDWXIOYVSVXLYWMWWKWX
      EQYWNYWTWWJFVURTYVSWXIWWPNWWNOLZOLZYYBWWPOLZYWDYVSWXGWWPWXHYYBOYVSWXGVWGW
      WJMLZVWFVXEWWIOLZMLZWWPYVSWXFWWJVWGMYVSXUFWWJPIZWXFWWJQVWBXUFXUTXUIWNYVSY
      WOYYHYWSWWJYIRZFWWJYPTVRYVSVWFWWJUPZMLZUIVWFWWJMLUDLZYYGYYEYVSVWFPIZYWOYY
      KYYLQYVSVXMYYMVWBVXMXUTVXOWNZVWFUTRZYWSVWFWWJVUSTYVSYYFYYJVWFMYVSXUJWWIPI
      ZYYFYYJQYAYVSYWPYYPYWRWWIYIRZKWWIVUTVEVRYVSYYMVWFJVGZYWMYYEYYLQYYOYVSVXMY
      YRYYNVWFVPRZYWTVWFWWJVVAVFYTYVSYYMYYRYXGWWIWEIZYYGWWPQYYOYYSYXGYVSYXHVDYV
      SYWPYYTYWRWWIYQRVWFVXEWWIVVFVSVVGYVSWXHWWNNOLZYYBYVSWXHNKWWMOLZUIVCLZMLZN
      UUUBMLZNOLZUUUAYVSWXDUUUCNMYVSWXDVXTWWJUKLZUUUCFVXTWWJUKGYEYVSUUUGVXSWWJU
      KLZUIVCLZUUUCYVSYUIXYSYYHUUUGUUUIQYVSVYAYUIVWBVYAXUTVYCWNYUJRXYSYVSYNVDYY
      IVXSUIWWJVVBVFYVSUUUBUUUHUIVCYVSXUJYUKYYPUUUBUUUHQXUJYVSYAVDVVSYUKVWAXUTY
      UMXBYYQKEWWIVVCVFWBVVDYOVRYVSVXKUUUBUQIZUUUDUUUFQVBYVSWYEYXBUUUJWOYXCKWWM
      WSVENUUUBVVEVEYVSUUUEWWNNOYVSUUUEXWTWWMMLZWWNYVSVXKWYEYXBUUUEUUUKQYYAWYEY
      VSWOVDYXCNKWWMYMVFXWTWWLWWMMYDYEVVHWBWIYVSYXOVXKUUUAYYBQYXRVBWWNNVVIYRWAV
      TYVSYYBPIZYXPYYDYYCQYVSVXKYXOUUULVBYXRNWWNVKVEYXSYYBWWPVVITYVSVXKYXOYXPYY
      DYWDQYYAYXRYXSNWWNWWPYSVFVVJVTWIWKYVSJSIYWIYWBWWQQVVKYXKJWWQVVLVEWJVVMWCV
      WBVWCVWLUAYVRXWBVVNVWBYXFVXFPIVXGWWRQYXIVXFUTAVXFWWHWWOAUGZWWIMLZOLZDULWW
      RPCUUUMVXFQZWWHUUUOWWQDUUUPUUUNWWPWWOOUUUMVXFWWIMVVPVRVVOHWWHWWQDVVQXPVAY
      TVWBVWSSIZVXASIZVXDVXAQVWBWVMWWBUUUQWVQWWCVWPVWRVVRTVWBWVSWWBUUURWVTWWCVW
      TVWRVVRTVWSVXAVVLTWC $.
      $( [30-Jul-2014] $)

    basel.t $e |- T = ( n e. ( 1 ... M ) |->
      ( ( tan ` ( ( n x. pi ) / N ) ) ^ -u 2 ) ) $.
    $( Lemma for ~ basel .  By ~ basellem3 , the expression
       ` P ( ( cot x ) ^ 2 ) = sin ( N x ) / ( sin x ) ^ N ` goes to zero
       whenever ` x = n pi / N ` for some ` n e. ( 1 ... M ) ` , so this
       function enumerates ` M ` distinct roots of a degree- ` M ` polynomial,
       which must therefore be all the roots by ~ fta1 . $)
    basellem4 $p |- ( M e. NN ->
      T : ( 1 ... M ) -1-1-onto-> ( `' P " { 0 } ) ) $=
      ( wcel co cc0 wbr cfv wceq c2 cc cr syl vx vy vk vm cfz ccnv csn cima wf1
      cn c1 cen cfn wf1o wf cv wi wral cpi cmul cdiv ctan cneg cexp wa wfn cply
      wb cdgr ccoe cn0 cbc cmin cmpt basellem2 simp1d plyf 3syl adantr fniniseg
      ffn 0cn sylancl crp cz cioo basellem1 tanrpcl 2z ax-mp rpexpcl rpre recnd
      znegcl csin basellem3 syldan wne elfzelz adantl zre remulcl caddc nnmulcl
      pire 2nn mpan peano2nn syl5eqel nnne0 divcan2 syl3anc fveq2d sinkpi eqtrd
      nncn oveq1d syl2anc nnnn0 clt simpld fmptd fveq2 sseli ad2antrl syl112anc
      ad2antll a1i mpbid cle rprege0 elrp oveq1 ovex fvmpt 2nn0 expneg cdom c0p
      chash nndivre resincl expcl ccos sincosq1sgn gt0ne0 nnz expne0i mpbir2and
      div0 3eqtrd ssriv ffvelrn sylan simplr pipos ltmul1 ad2antrr nngt0 ltdiv1
      ad2ant2r ad2ant2rl tanord lt2sq syl2an ltrec syl2anb 3brtr4d an32s eqord2
      nnre biimprd ralrimivva dff13 sylanbrc simp2d eqnetrd dgr0 syl6eq necon3i
      f1dom eqid fta1 simprd hashfz1 eqtr4d breqtrd fzfid hashdom sbth f1finf1o
      ex ) FUJKZUKFUELZBUFMUGUHZCUIZUWNUWOULNZUWOUMKZUWNUWOCUNUWMUWNUWOCUOUAUPZ
      COZUBUPZCOZPZUWSUXAPZUQZUBUWNURUAUWNURUWPUWMEUWNEUPZUSUTLZGVALZVBOZQVCZVD
      LZUWOCUWMUXFUWNKZVEZUXKUWOKZUXKRKZUXKBOZMPZUXMBRVFZMRKUXNUXOUXQVEVHUWMUXR
      UXLUWMBRVGOKZRRBUOUXRUWMUXSBVIOZFPZBVJOEVKGQUXFUTLVLLUKVCFUXFVMLVDLUTLVNP
      ZABDEFGHIVOZVPZRBVQRRBWAVRVSWBRMUXKBRVTWCUXMUXKUXMUXKWDKZUXKSKUXMUXIWDKZU
      XJWEKZUYEUXMUXHMUSQVALWFLZKZUYFUXFFGHWGZUXHWHTQWEKZUYGWIQWNWJUXIUXJWKWCUX
      KWLTZWMUXMUXPGUXHUTLZWOOZUXHWOOZGVDLZVALZMUYPVALZMUWMUXLUYIUXPUYQPUYJAUXH
      BDFGHIWPWQUXMUYNMUYPVAUXMUYNUXGWOOZMUXMUYMUXGWOUXMUXGRKGRKZGMWRZUYMUXGPUX
      MUXGUXMUXFSKZUSSKZUXGSKZUXMUXFWEKZVUBUXLVUEUWMUXFUKFWSZWTZUXFXAZTXEUXFUSX
      BWCZWMUXMGUJKZUYTUWMVUJUXLUWMGQFUTLZUKXCLZUJHUWMVUKUJKZVULUJKQUJKUWMVUMXF
      QFXDXGVUKXHTXIZVSZGXPTUXMVUJVUAVUOGXJTUXGGXKXLXMUXMVUEUYSMPVUGUXFXNTXOXQU
      XMUYPRKZUYPMWRZUYRMPUXMUYORKZGVKKZVUPUXMUYOUXMUXHSKZUYOSKZUXMVUDVUJVUTVUI
      VUOUXGGUUAXRUXHUUBTZWMZUXMVUJVUSVUOGXSTUYOGUUCXRUXMVURUYOMWRZGWEKZVUQVVCU
      XMVVAMUYOXTNZVVDVVBUXMVVFMUXHUUDOXTNZUXMUYIVVFVVGVEUYJUXHUUETYAUYOUUFXRUX
      MVUJVVEVUOGUUGTUYOGUUHXLUYPUUJXRUUKUUIJYBUWMUXEUAUBUWNUWNUWMUWSUWNKUXAUWN
      KVEVEUXDUXCUWMUCUDUCUPZCOZUDUPZCOZUWSUXAUWNUWTUXBVVHVVJCYCVVHUWSCYCVVHUXA
      CYCEUWNSUXLVUEVUBVUFVUHTUULZUWMUWNSCUOVVHUWNKZVVISKUWMEUWNUXKSCUYLJYBUWNS
      VVHCUUMUUNUWMVVMVVJUWNKZVEZVEVVHVVJXTNZVVKVVIXTNZUWMVVPVVOVVQUWMVVPVEZVVO
      VEZUKVVJUSUTLZGVALZVBOZQVDLZVALZUKVVHUSUTLZGVALZVBOZQVDLZVALZVVKVVIXTVVSV
      WHVWCXTNZVWDVWIXTNZVVSVWGVWBXTNZVWJVVSVWFVWAXTNZVWLVVSVWEVVTXTNZVWMVVSVVP
      VWNUWMVVPVVOUUOVVSVVHSKZVVJSKZVUCMUSXTNZVVPVWNVHVVMVWOVVRVVNUWNSVVHVVLYDY
      EZVVNVWPVVRVVMUWNSVVJVVLYDYGZVUCVVSXEYHVWQVVSUUPYHVVHVVJUSUUQYFYIVVSVWESK
      ZVVTSKZGSKZMGXTNZVWNVWMVHVVSVWOVUCVWTVWRXEVVHUSXBWCVVSVWPVUCVXAVWSXEVVJUS
      XBWCVVSVUJVXBUWMVUJVVPVVOVUNUURZGUVKTVVSVUJVXCVXDGUUSTVWEVVTGUUTYFYIVVSVW
      FUYHKZVWAUYHKZVWMVWLVHUWMVVMVXEVVPVVNVVHFGHWGUVAZUWMVVNVXFVVPVVMVVJFGHWGU
      VBZVWFVWAUVCXRYIVVSVWGWDKZVWBWDKZVWLVWJVHZVVSVXEVXIVXGVWFWHTZVVSVXFVXJVXH
      VWAWHTZVXIVWGSKZMVWGYJNVEVWBSKZMVWBYJNVEVXKVXJVWGYKVWBYKVWGVWBUVDUVEXRYIV
      VSVWHWDKZVWCWDKZVWJVWKVHZVVSVXIUYKVXPVXLWIVWGQWKWCVVSVXJUYKVXQVXMWIVWBQWK
      WCVXPVWHSKMVWHXTNVEVWCSKMVWCXTNVEVXRVXQVWHYLVWCYLVWHVWCUVFUVGXRYIVVSVVKVW
      BUXJVDLZVWDVVNVVKVXSPVVRVVMEVVJUXKVXSUWNCUXFVVJPZUXIVWBUXJVDVXTUXHVWAVBVX
      TUXGVVTGVAUXFVVJUSUTYMXQXMXQJVWBUXJVDYNYOYGVVSVWBRKQVKKZVXSVWDPVVSVWBVVSV
      XJVXOVXMVWBWLTWMYPVWBQYQWCXOVVSVVIVWGUXJVDLZVWIVVMVVIVYBPVVRVVNEVVHUXKVYB
      UWNCUXFVVHPZUXIVWGUXJVDVYCUXHVWFVBVYCUXGVWEGVAUXFVVHUSUTYMXQXMXQJVWGUXJVD
      YNYOYEVVSVWGRKVYAVYBVWIPVVSVWGVVSVXIVXNVXLVWGWLTWMYPVWGQYQWCXOUVHUVIUWLUV
      JUVLUVMUAUBUWNUWOCUVNUVOZUWMUWNUWOYRNZUWOUWNYRNZUWQUWMUWPVYEVYDUWNUWOCUKF
      UEYNUWATUWMUWOYTOZUWNYTOZYJNZVYFUWMVYGUXTVYHYJUWMUWRVYGUXTYJNZUWMUXSBYSWR
      ZUWRVYJVEUYDUWMUXTMWRVYKUWMUXTFMUWMUXSUYAUYBUYCUVPZFXJUVQBYSUXTMBYSPUXTYS
      VIOMBYSVIYCUVRUVSUVTTUWORBUWOUWBUWCXRZUWDUWMUXTFVYHVYLUWMFVKKVYHFPFXSFUWE
      TUWFUWGUWMUWRUWNUMKVYIVYFVHUWMUWRVYJVYMYAZUWMUKFUWHUWOUWNUWIXRYIUWNUWOUWJ
      XRVYNUWNUWOCUWKXL $.
      $( [28-Jul-2014] $)

    $( Lemma for ~ basel .  Using ~ vieta1 , we can calculate the sum of the
       roots of ` P ` as the quotient of the top two coefficients, and since
       the function ` T ` enumerates the roots, we are left with an equation
       that sums the ` cot ^ 2 ` function at the ` M ` different roots. $)
    basellem5 $p |- ( M e. NN -> sum_ k e. ( 1 ... M )
      ( ( tan ` ( ( k x. pi ) / N ) ) ^ -u 2 ) =
      ( ( ( 2 x. M ) x. ( ( 2 x. M ) - 1 ) ) / 6 ) ) $=
      ( wcel c1 co cdiv cmul c2 cc wceq syl vx cn ccnv cc0 csn cima cv csu cdgr
      cfv cmin ccoe cneg cfz cpi ctan cexp c6 eqid cply cn0 cbc basellem2 chash
      cmpt simp1d simp2d nnnn0 hashfz1 cen wbr wf1o basellem4 ovex f1oen cfn wb
      fzfid cle c0p wne wa nnne0 eqnetrd fveq2 dgr0 syl6eq necon3i fta1 syl2anc
      simpld hashen mpbird 3eqtr2rd id eqeltrd vieta1 oveq1 oveq1d 3syl fveq12d
      fvmpt nnm1nn0 oveq2 oveq2d oveq12d nncn ax-1cn sylancl ax-mp cz caddc 2nn
      nnmulcl nnz sylancr mulcom 3eqtrd nn0uz cr nnre nnuz syl6eleq elfz5 bccl2
      cuz eqtrd syl3anc c3 bcm1k a1i nncni nndivre recnd jca clt recdiv 3eqtr3d
      syl22anc 6nn fveq2d adantl cdm cnvimass wf plyf fdm sselda fsumf1o simp3d
      syl5sseq nncan negcli exp1 mpan peano2nn syl5eqel 2z peano2zm zmulcl bccl
      nn0cn mulm1 negcl subid exp0 wss 1nn0 eleqtri fzss1 lep1 syl6breqr sseldd
      mulid1 divneg negeqd negneg pnncan oveq1i df-2 3eqtr4g 2nn0 nn0sub 2times
      addsub nn0nnaddcl mpancom 2timesi eqcomi oveq2i subsub4 subdi subsub df-3
      syl6eqel 3eqtr4a syl6eqr 3re 2re mulass 3cn divmuldiv 3t2e6 eqeltrrd ltm1
      6re nn0re eqbrtrrd wi ltle mpd letrd divcan3 nnne0i mpanl12 ) GUBLZBUCUDU
      EZUFZUAUGZUAUHBUIUJZMUKNZBULUJZUJZUXTUYBUJZONUMZMGUNNZEUGZUOPNZHONZUPUJZQ
      UMZUQNZEUHQGPNZUYMMUKNZPNZURONZUXPUAUYBUXRRBUXTUYBUSUXTUSUXRUSZUXPBRUTUJL
      ZUXTGSZUYBFVAHQFUGZPNZVBNZMUMZGUYTUKNZUQNZPNZVEZSZABDFGHIJVCZVFZUXPUXTGUY
      FVDUJZUXRVDUJZUXPUYRUYSVUHVUIVGZUXPGVALZVUKGSGVHZGVITUXPVUKVULSZUYFUXRVJV
      KZUXPUYFUXRCVLVUQABCDFGHIJKVMZUYFUXRCMGUNVNVOTUXPUYFVPLUXRVPLZVUPVUQVQUXP
      MGVRZUXPVUSVULUXTVSVKZUXPUYRBVTWAZVUSVVAWBVUJUXPUXTUDWAVVBUXPUXTGUDVUMGWC
      WDBVTUXTUDBVTSUXTVTUIUJUDBVTUIWEWFWGWHTUXRRBUYQWIWJWKUYFUXRWLWJWMWNUXPUXT
      GUBVUMUXPWOWPWQUXPUXRUXSUYFUYLUAECUYLUXSUYLSWOVUTVURUYGUYFLUYGCUJUYLSUXPF
      UYGUYTUOPNZHONZUPUJZUYKUQNUYLUYFCUYTUYGSZVVEUYJUYKUQVVFVVDUYIUPVVFVVCUYHH
      OUYTUYGUOPWRWSUUAWSKUYJUYKUQVNXBUUBUXPUXRRUXSUXPBUUCZUXRRBUXQUUDUXPUYRRRB
      UUEVVGRSVUJRBUUFRRBUUGWTUUKUUHUUIUXPUYEUYCUMZUYDONZHQGMUKNZPNZVBNZHUYMVBN
      ZONZUYPUXPUYCRLUYDRLUYDUDWAUYEVVISUXPUYCVVLUMZRUXPUYCVVJVUGUJZVVLVUCGVVJU
      KNZUQNZPNZVVOUXPUYAVVJUYBVUGUXPUYRUYSVUHVUIUUJZUXPUXTGMUKVUMWSXAUXPVVJVAL
      ZVVPVVSSGXCZFVVJVUFVVSVAVUGUYTVVJSZVUBVVLVUEVVRPVWCVUAVVKHVBUYTVVJQPXDXEV
      WCVUDVVQVUCUQUYTVVJGUKXDXEXFVUGUSZVVLVVRPVNXBTUXPVVSVVLVUCPNZVUCVVLPNZVVO
      UXPVVRVUCVVLPUXPVVRVUCMUQNZVUCUXPVVQMVUCUQUXPGRLZMRLZVVQMSGXGZXHGMUULXIXE
      VUCRLZVWGVUCSMXHUUMZVUCUUNXJWGXEUXPVVLRLZVWKVWEVWFSUXPVVLVALZVWMUXPHVALZV
      VKXKLZVWNUXPHUBLZVWOUXPHUYMMXLNZUBIUXPUYMUBLZVWRUBLQUBLUXPVWSXMQGXNUUOZUY
      MUUPTUUQZHVHTZUXPQXKLVVJXKLZVWPUURUXPGXKLVXCGXOGUUSTQVVJUUTXPVVKHUVAWJVVL
      UVBTZVWLVVLVUCXQXIUXPVWMVWFVVOSVXDVVLUVCTXRXRZUXPVWMVVORLVXDVVLUVDTWPUXPU
      YDVVMRUXPUYDGVUGUJZVVMVUCGGUKNZUQNZPNZVVMUXPUXTGUYBVUGVVTVUMXAUXPVUNVXFVX
      ISVUOFGVUFVXIVAVUGUYTGSZVUBVVMVUEVXHPVXJVUAUYMHVBUYTGQPXDXEVXJVUDVXGVUCUQ
      UYTGGUKXDXEXFVWDVVMVXHPVNXBTUXPVXIVVMMPNZVVMUXPVXHMVVMPUXPVXHVUCUDUQNZMUX
      PVXGUDVUCUQUXPVWHVXGUDSVWJGUVETXEVWKVXLMSVWLVUCUVFXJWGXEUXPVVMRLZVXKVVMSU
      XPVVMUBLZVXMUXPVWQUYMUDHUNNZLVXNVXAUXPMHUNNZVXOUYMUXPMUDYFUJZLVWQVXPVXOUV
      GMVAVXQUVHXSUVIVXAMUDHUBUVJXPUXPUYMVXPLZUYMHVSVKZUXPUYMVWRHVSUXPVWSUYMXTL
      UYMVWRVSVKVWTUYMYAUYMUVKWTIUVLUXPUYMMYFUJZLHXKLZVXRVXSVQUXPUYMUBVXTVWTYBY
      CUXPVWQVYAVXAHXOTZUYMMHYDWJWMZUVMUYMHUBYEWJZVVMXGTZVVMUVNTYGXRZVYEWPUXPUY
      DVVMUDVYFUXPVXNVVMUDWAZVYDVVMWCTZWDUYCUYDUVOYHUXPVVHVVLUYDVVMOUXPVVHVVOUM
      ZVVLUXPUYCVVOVXEUVPUXPVWMVYIVVLSVXDVVLUVQTYGVYFXFUXPMVVMVVLONZONZMURUYOON
      ZONZVVNUYPUXPVYJVYLMOUXPVYJVVLVYLPNZVVLONZVYLUXPVVMVYNVVLOUXPVVMVVLYIUYNO
      NZQUYMONZPNZPNZVYNUXPVVMHUYNVBNZHUYNUKNZUYMONZPNZVVLVYPPNZVYQPNZVYSUXPVWQ
      VXRVVMWUCSVXAVYCUYMHUBYJWJUXPVYTWUDWUBVYQPUXPVYTHUYNMUKNZVBNZHWUFUKNZUYNO
      NZPNZWUDUXPVWQUYNVXPLZVYTWUJSVXAUXPWUKUYNHVSVKZUXPWULWUAVALZUXPWUAQVAUXPV
      WRUYNUKNZMMXLNZWUAQUXPUYMRLZVWIVWIWUNWUOSUXPVWSWUPVWTUYMXGTZVWIUXPXHYKZWU
      RUYMMMUVRYHHVWRUYNUKIUVSUVTUWAZUWBUWOUXPUYNVALZVWOWULWUMVQUXPVWSWUTVWTUYM
      XCTVXBUYNHUWCWJWMZUXPUYNVXTLVYAWUKWULVQUXPUYNUBVXTUXPUYNVVJGXLNZUBUXPUYNG
      GXLNZMUKNZWVBUXPUYMWVCMUKUXPVWHUYMWVCSVWJGUWDTWSUXPVWHVWHVWIWVDWVBSVWJVWJ
      WURGGMUWEYHYGVWAUXPWVBUBLVWBVVJGUWFUWGWPZYBYCVYBUYNMHYDWJWMUYNHUBYJWJUXPW
      UGVVLWUIVYPPUXPWUFVVKHVBUXPUYMWUOUKNZUYMQMPNZUKNZWUFVVKWUOWVGUYMUKWVGWUOM
      XHUWHUWIUWJUXPWUPVWIVWIWUFWVFSWUQWURWURUYMMMUWKYHUXPQRLZVWHVWIVVKWVHSWVIU
      XPQXMYLYKZVWJWURQGMUWLYHUWPZXEUXPWUHYIUYNOUXPWUHWUAMXLNZYIUXPHRLZUYNRLZVW
      IWUHWVLSUXPVWQWVMVXAHXGTUXPUYNUBLZWVNWVEUYNXGTZWURHUYNMUWMYHUXPWVLQMXLNYI
      UXPWUAQMXLWUSWSUWNUWQYGWSXFYGUXPWUAQUYMOWUSWSXFUXPVWMVYPRLVYQRLWUEVYSSVXD
      UXPVYPUXPYIXTLWVOVYPXTLUWRWVEYIUYNYMXPYNUXPVYQUXPQXTLVWSVYQXTLUWSVWTQUYMY
      MXPYNVVLVYPVYQUWTYHXRUXPVYRVYLVVLPUXPVYRYIQPNZUYNUYMPNZONZVYLUXPYIRLZWVIW
      VNUYNUDWAZWBWUPUYMUDWAZWBVYRWVSSWVTUXPUXAYKWVJUXPWVNWWAWVPUXPWVOWWAWVEUYN
      WCTYOUXPWUPWWBWUQUXPVWSWWBVWTUYMWCTYOYIQUYNUYMUXBYSUXPWVQURWVRUYOOWVQURSU
      XPUXCYKUXPWVNWUPWVRUYOSWVPWUQUYNUYMXQWJXFYGXEYGWSUXPVYLRLVWMVVLUDWAZVYOVY
      LSUXPVYLUXPURXTLUYOUBLZVYLXTLUXFUXPVWSWVOWWDVWTWVEUYMUYNXNWJZURUYOYMXPYNV
      XDUXPVVLUBLZWWCUXPVWQVVKVXOLZWWFVXAUXPWWGVVKHVSVKZUXPVVKUYNHUXPVVKVALVVKX
      TLZUXPWUFVVKVAWVKUXPWVOWUFVALWVEUYNXCTUXDZVVKUXGTZUXPWVOUYNXTLZWVEUYNYATZ
      UXPVWQHXTLVXAHYATUXPVVKUYNYPVKZVVKUYNVSVKZUXPWUFVVKUYNYPWVKUXPWWLWUFUYNYP
      VKWWMUYNUXETUXHUXPWWIWWLWWNWWOUXIWWKWWMVVKUYNUXJWJUXKWVAUXLUXPVVKVXQLVYAW
      WGWWHVQUXPVVKVAVXQWWJXSYCVYBVVKUDHYDWJWMVVKHUBYEWJVVLWCTZVYLVVLUXMYHYGXEU
      XPVXMVYGVWMWWCVYKVVNSVYEVYHVXDWWPVVMVVLYQYSUXPUYORLZUYOUDWAZVYMUYPSZUXPWW
      DWWQWWEUYOXGTUXPWWDWWRWWEUYOWCTURRLURUDWAWWQWWRWBWWSURYTYLURYTUXNURUYOYQU
      XOWJYRXRYR $.
      $( [29-Jul-2014] $)
  $}

  ${
    $d k n x y F $.  $d k x y z G $.  $d k x y z H $.  $d j k n x M $.
    $d k x y A $.  $d k n y J $.  $d k x y K $.  $d j k n x N $.
    basel.g $e |- G = ( n e. NN |-> ( 1 / ( ( 2 x. n ) + 1 ) ) ) $.
    $( Lemma for ~ basel .  The function ` G ` goes to zero because it is
       bounded by ` 1 / n ` . $)
    basellem6 $p |- G ~~> 0 $=
      ( cc0 wbr wtru cn c1 cdiv co cc wcel a1i c2 caddc cr wceq adantl syl cle
      vk cli cv cmpt cvv nnuz cz 1z ax-1cn divcnv ax-mp cmul nnex mptex eqeltri
      0cn cfv oveq2 eqid ovex fvmpt nnrecre eqeltrd oveq1d oveq2d nnmulcl sylan
      2nn peano2nn nnre cn0 nnnn0 nn0addge1 syl2anc recnd 2times breqtrrd letrd
      wa lep1 clt wb nngt0 lerec syl22anc mpbid 3brtr4d nnrp rpreccl rpge0 3syl
      crp climsqueeze2 trud ) BDUBEFDUAAGHAUCZIJZUDZBHKUEGUFHUGLFUHMDKLFUPMWQDU
      BEZFHKLWRUIHAUJUKMBUELFBAGHNWOULJZHOJZIJZUDUECAGXAUMUNUOMFUAUCZGLZVSZXBWQ
      UQZHXBIJZPXCXEXFQFAXBWPXFGWQWOXBHIURWQUSHXBIUTVARZXCXFPLFXBVBRVCXDXBBUQZH
      NXBULJZHOJZIJZPXCXHXKQFAXBXAXKGBWOXBQZWTXJHIXLWSXIHOWOXBNULURVDVECHXJIUTV
      ARZXDXJGLZXKPLXDXIGLZXNFNGLZXCXOXPFVHMNXBVFVGZXIVISZXJVBSVCXDXKXFXHXETXDX
      BXJTEZXKXFTEZXDXBXIXJXCXBPLZFXBVJRZXDXOXIPLZXQXIVJSZXDXNXJPLZXRXJVJSZXDXB
      XBXBOJZXITXDYAXBVKLZXBYGTEYBXCYHFXBVLRXBXBVMVNXDXBKLXIYGQXDXBYBVOXBVPSVQX
      DYCXIXJTEYDXIVTSVRXDYADXBWAEZYEDXJWAEZXSXTWBYBXCYIFXBWCRYFXDXNYJXRXJWCSXB
      XJWDWEWFXMXGWGXDDXKXHTXDXJWLLZXKWLLDXKTEXDXNYKXRXJWHSXJWIXKWJWKXMVQWMWN
      $.
      $( [28-Jul-2014] $)

    ${
      basellem7.2 $e |- A e. CC $.
      $( Lemma for ~ basel .  The function ` 1 + A x. G ` for any fixed ` A `
         goes to ` 1 ` . $)
      basellem7 $p |- ( ( NN X. { 1 } ) oF +
        ( ( NN X. { A } ) oF x. G ) ) ~~> 1 $=
        ( cn c1 cmul co caddc cc0 cli wtru cc cvv wcel a1i cfv wf sylan csn cxp
        vk vx vy cof wbr cz nnuz 1z ax-1cn cuz eqimss2i nnex climconst2 sylancr
        0cn ovex basellem6 cv wss elexi fconst snssd fss ffvelrn c2 cdiv wa 2nn
        nnmulcl peano2nn nnrecre 3syl recnd fmptd wfn ffn syl inidm eqidd ofval
        cr climmul mul01i syl6breq 1nn mulcl adantl off climadd addid1i breqtri
        trud ) FGUAZUBZFAUAZUBZCHUFZIZJUFZIZGKJIZGLXBXCLUGMGKUCWPWTXBGUHNOFUIGU
        HPZMUJQZXEMGNPZXDWPGLUGUKXEGGFFGULRUIUMZUNUOUPKNPMUQQZXBOPMWPWTXAURQMWT
        AKHIKLMAKUCWRCWTGNNOFUIXEANPZMEQZMXIXDWRALUGEXEAGFXGUNUOUPXHWTOPMWRCWSU
        RQCKLUGMBCDUSQMFNWRSZUCUTZFPZXLWRRZNPMFWQWRSWQNVAXKFAANEVBVCMANXJVDFWQN
        WRVEUPZFNXLWRVFTMFNCSZXMXLCRZNPMBFGVGBUTZHIZGJIZVHIZNCMXRFPZVIZYAYCXSFP
        ZXTFPYAWCPMVGFPZYBYDYEMVJQVGXRVKTXSVLXTVMVNVODVPZFNXLCVFTMFFXNXQHFWRCOO
        XLMXKWRFVQXOFNWRVRVSMXPCFVQYFFNCVRVSFOPMUNQZYGFVTZMXMVIZXNWAYIXQWAWBWDA
        EWEWFMFNWPSZXMXLWPRZNPMFWOWPSZWONVAYJFGGFWGVBVCZMGNXFMUKQVDFWONWPVEUPFN
        XLWPVFTMFNWTSZXMXLWTRZNPMUDUEFFFHNNNWRCOOUDUTZNPUEUTZNPVIYPYQHINPMYPYQW
        HWIXOYFYGYGYHWJZFNXLWTVFTMFFYKYOJFWPWTOOXLMYLWPFVQYLMYMQFWOWPVRVSMYNWTF
        VQYRFNWTVRVSYGYGYHYIYKWAYIYOWAWBWKWNGUKWLWM $.
        $( [28-Jul-2014] $)
    $}

    basel.f $e |- F = seq 1 ( + , ( n e. NN |-> ( n ^ -u 2 ) ) ) $.
    basel.h $e |- H = ( ( NN X. { ( ( pi ^ 2 ) / 6 ) } ) oF x.
                        ( ( NN X. { 1 } ) oF - G ) ) $.
    basel.j $e |- J = ( H oF x.
      ( ( NN X. { 1 } ) oF + ( ( NN X. { -u 2 } ) oF x. G ) ) ) $.
    basel.k $e |- K = ( H oF x. ( ( NN X. { 1 } ) oF + G ) ) $.
    ${
      basellem8.n $e |- N = ( ( 2 x. M ) + 1 ) $.
      $( Lemma for ~ basel .  The function ` F ` of partial sums of the inverse
         squares is bounded below by ` J ` and above by ` K ` , obtained by
         summing the inequality ` cot ^ 2 x <_ 1 / x ^ 2 <_ `
         ` csc ^ 2 x = cot ^ 2 x + 1 ` over the ` M ` roots of the polynomial
         ` P ` , and applying the identity ~ basellem5 . $)
      basellem8 $p |- ( M e. NN ->
        ( ( J ` M ) <_ ( F ` M ) /\ ( F ` M ) <_ ( K ` M ) ) ) $=
        ( wcel c1 co cdiv cmul wceq vk vx vj cn cfv cle wbr cfz c2 cexp cv ctan
        cpi cneg csu wa cr pire caddc syl sylancr resqcl adantr cc0 wne cz rpre
        crp rpne0 2z a1i reexpclz syl3anc remulcl syl2anc nnne0 cc recnd expneg
        2nn0 sylancl oveq2d sqcl rpexpcl divrec eqtr4d oveq2i nncn oveq1d recni
        cn0 2cn jca eqtrd 3eqtr4d clt simpld elrp sylanbrc rpge0 syl22anc mpbid
        wb wi ltle mpd eqbrtrd rpgt0 fsumle c6 cmin syl6eqr oveq12d csn cxp cof
        cmpt wtru cvv ovex 6re fconstmpt offval2 syl5eq trud eqtri fvmpt ax-1cn
        divcl mulass oveq1i pncan divid divdir mulcl divmuldiv 3eqtrd syl112anc
        eqid c3 fzfid 2nn nnmulcl mpan peano2nn syl5eqel nndivre cioo basellem1
        tanrpcl znegcl ax-mp elfznn adantl nnre negnegi expnegz syl5reqr expclz
        expne0i divrec2 divass sqmul tangtx elioore eliooord lt2sq nnrp lediv23
        syl122anc oveq2 nnex resqcli 6nn nnne0i redivcli negcli subcl divsubdir
        1z 3eqtr3rd negsub oveq12i pnpcan2 nnsqcl mulcom pm3.2i sqdiv sqval cbc
        df-2 basellem5 fsummulc2 cseq oveq1 cuz id nnuz syl6eleq fsumser fveq1i
        syl6reqr 3brtr4d csin ccos sincosq1sgn gt0ne0 sinltx 0re le2sq breqtrrd
        resincl lemuldiv2 lemuldiv bitr3d peano2cn eqtr2d recoscl simprd tanval
        sincossq sqne0 mpbird recdiv 3eqtrrd 3eqtr3d sumeq2dv fsumadd chash cfn
        addcom fsumconst nnnn0 hashfz1 mulid1 adddi df-3 3eqtri subadd23 addass
        3cn mp2an 3eqtr4a mul32 3t2e6 mulcomi eqtr3i divcan3 ) GUDOZGEUEZGBUEZU
        FUGVUKGFUEZUFUGVUIPGUHQZUMHRQZUIUJQZUAUKZUMSQHRQZULUEZUIUNZUJQZSQZUAUOZ
        VUMVUPVUSUJQZUAUOZVUJVUKUFVUIVUMVVAVVCUAVUIPGUUAZVUIVUPVUMOZUPZVUOUQOZV
        UTUQOZVVAUQOVUIVVHVVFVUIVUNUQOZVVHVUIUMUQOHUDOZVVJURVUIHUIGSQZPUSQZUDNV
        UIVVLUDOZVVMUDOUIUDOVUIVVNUUBUIGUUCUUDZVVLUUEUTUUFZUMHUUGVAZVUNVBUTVCZV
        VGVURUQOZVURVDVEZVUSVFOZVVIVVGVURVHOZVVSVVGVUQVDUMUIRQZUUHQOZVWBVUPGHNU
        UIZVUQUUJUTZVURVGUTZVVGVWBVVTVWFVURVIUTVWAVVGUIVFOZVWAVJUIUUKUULZVKZVUR
        VUSVLVMZVUOVUTVNVOVVGVUPUQOZVUPVDVEZVWAVVCUQOZVVGVUPUDOZVWLVVFVWOVUIVUP
        GUUMUUNZVUPUUOUTVVGVWOVWMVWPVUPVPZUTZVWJVUPVUSVLVMZVVGVVAVUOVURUIUJQZRQ
        ZVVCUFVVGVVAVUOPVWTRQZSQZVXAVVGVUTVXBVUOSVVGVURVQOUIWKOZVUTVXBTVVGVURVW
        GVRVTVURUIVSWAZWBVVGVUOVQOZVWTVQOVWTVDVEZVXAVXCTVUIVXFVVFVUIVUNVQOZVXFV
        UIVUNVVQVRZVUNWCUTZVCZVVGVWTVVGVWTVHOZVWTUQOZVVGVWBVWHVXLVWFVJVURUIWDWA
        ZVWTVGUTZVRVVGVXLVXGVXNVWTVIUTVUOVWTWEVMWFVVGVUOVVCRQZVWTUFUGZVXAVVCUFU
        GZVVGVXPVUQUIUJQZVWTUFVVGPVVCRQZVUOSQZVUPUIUJQZVUOSQZVXPVXSVVGVXTVYBVUO
        SVVGVYBVUPVUSUNZUJQZVXTVYDUIVUPUJUIWLUUPWGVVGVUPVQOZVWMVWAVYEVXTTVVGVWO
        VYFVWPVUPWHZUTZVWRVWJVUPVUSUUQVMUURWIVVGVXFVVCVQOZVVCVDVEZVXPVYATVXKVVG
        VWOVYIVWPVWOVYFVWMVWAVYIVYGVWQVWAVWOVWIVKVUPVUSUUSVMUTZVVGVYFVWMVWAVYJV
        YHVWRVWJVUPVUSUUTVMVUOVVCUVAVMVVGVXSVUPVUNSQZUIUJQZVYCVVGVUQVYLUIUJVVGV
        YFUMVQOZHVQOZHVDVEZUPZVUQVYLTVYHVYNVVGUMURWJZVKVUIVYQVVFVUIVYOVYPVUIVVK
        VYOVVPHWHUTZVUIVVKVYPVVPHVPUTZWMZVCVUPUMHUVBVMWIVVGVYFVXHVYMVYCTVYHVUIV
        XHVVFVXIVCVUPVUNUVCVOWNWOZVVGVXSVWTWPUGZVXSVWTUFUGZVVGVUQVURWPUGZWUCVVG
        VWDWUEVWEVUQUVDUTVVGVUQUQOZVDVUQUFUGZVVSVDVURUFUGZWUEWUCXCVVGVWDWUFVWEV
        UQVDVWCUVEUTZVVGVUQVHOZWUGVVGWUFVDVUQWPUGZWUJWUIVVGWUKVUQVWCWPUGZVVGVWD
        WUKWULUPVWEVUQVDVWCUVFUTWQVUQWRWSZVUQWTUTZVWGVVGVWBWUHVWFVURWTUTVUQVURU
        VGXAXBVVGVXSUQOZVXMWUCWUDXDVVGWUFWUOWUIVUQVBUTVXOVXSVWTXEVOXFXGVVGVVHVW
        NVDVVCWPUGZVXMVDVWTWPUGZVXQVXRXCVVRVWSVVGVVCVHOZWUPVVGVUPVHOZVWAWURVVGV
        WOWUSVWPVUPUVHUTVWIVUPVUSWDWAVVCXHUTZVXOVVGVXLWUQVXNVWTXHUTVUOVVCVWTUVI
        UVJXBXGXIVUIVUJUMUIUJQZXJRQZPPHRQZXKQZSQZPVUSWVCSQZUSQZSQZVUOVUMVUTUAUO
        ZSQZVVBAGWVBPPUIAUKZSQZPUSQZRQZXKQZSQZPVUSWVNSQZUSQZSQZWVHUDEWVKGTZWVPW
        VEWVRWVGSWVTWVOWVDWVBSWVTWVNWVCPXKWVTWVMHPRWVTWVMVVMHWVTWVLVVLPUSWVKGUI
        SUVKWINXLWBZWBWBZWVTWVQWVFPUSWVTWVNWVCVUSSWWAWBWBXMEDUDPXNXOZUDVUSXNXOZ
        CSXPZQZUSXPZQZWWEQZAUDWVSXQZLWWIWWJTXRAUDWVPWVRSDWWHXSXSXSUDXSOXRUVLVKZ
        WVPXSOXRWVKUDOUPZWVBWVOSXTVKZWVRXSOWWLPWVQUSXTVKXRDUDWVBXNXOZWWCCXKXPQZ
        WWEQAUDWVPXQKXRAUDWVBWVOSWWNWWOXSUQXSWWKWVBUQOWWLWVAXJUMURUVMZYAXJUVNUV
        OZUVPZVKWVOXSOWWLPWVNXKXTVKWWNAUDWVBXQTXRAUDWVBYBVKXRAUDPWVNXKWWCCXSVFX
        SWWKPVFOWWLUVTVKZWVNXSOWWLPWVMRXTVKZWWCAUDPXQTXRAUDPYBVKZCAUDWVNXQTXRIV
        KZYCYCYDZXRAUDPWVQUSWWCWWFXSVFXSWWKWWSWVQXSOWWLVUSWVNSXTVKWXAXRAUDVUSWV
        NSWWDCXSVQXSWWKVUSVQOZWWLUIWLUVQZVKWWTWWDAUDVUSXQTXRAUDVUSYBVKWXBYCYCYC
        YEYFWVEWVGSXTYGVUIWVHVUOVVLVVLPXKQZSQZXJRQZSQZWVJVUIWVBVVLHRQZSQZWXFHRQ
        ZSQZWVBWXJWXLSQZSQZWVHWXIVUIWVBVQOZWXJVQOZWXLVQOZWXMWXOTWXPVUIWVBWWRWJV
        KZVUIVVLVQOZVYOVYPWXQVUIVVNWXTVVOVVLWHUTZVYSVYTVVLHYIVMZVUIWXFVQOZVYOVY
        PWXRVUIWXTPVQOZWYCWYAYHVVLPUVRWAZVYSVYTWXFHYIVMWVBWXJWXLYJVMVUIWVEWXKWV
        GWXLSVUIWVDWXJWVBSVUIHPXKQZHRQZHHRQZWVCXKQZWXJWVDVUIVYOWYDVYQWYGWYITVYS
        WYDVUIYHVKZWUAHPHUVSVMVUIWYFVVLHRVUIWYFVVMPXKQZVVLHVVMPXKNYKVUIWXTWYDWY
        KVVLTWYAYHVVLPYLWAYDWIVUIWYHPWVCXKVUIVYOVYPWYHPTVYSVYTHYMVOZWIUWAWBZVUI
        HVUSUSQZHRQZWYHVUSHRQZUSQZWXLWVGVUIVYOWXDVYQWYOWYQTVYSWXDVUIWXEVKZWUAHV
        USHYNVMVUIWYNWXFHRVUIWYNHUIXKQZWXFVUIVYOUIVQOZWYNWYSTVYSWLHUIUWBWAVUIWY
        SVVMPPUSQZXKQZWXFHVVMUIXUAXKNUWKUWCVUIWXTWYDWYDXUBWXFTWYAWYJWYJVVLPPUWD
        VMYDWNWIVUIWYHPWYPWVFUSWYLVUIWXDVYOVYPWYPWVFTWYRVYSVYTVUSHWEVMXMUWAXMVU
        IWVAHUIUJQZRQZWXHSQZWVBWXGXUCRQZSQZWXIWXOVUIWVAWXGSQZXUCXJSQZRQZXUHXJXU
        CSQZRQZXUEXUGVUIXUIXUKXUHRVUIXUCVQOZXJVQOZXUIXUKTVUIXUCUDOZXUMVUIVVKXUO
        VVPHUWEUTZXUCWHUTZXJYAWJZXUCXJUWFWAZWBVUIWVAVQOZWXGVQOZXUMXUCVDVEZUPZXU
        NXJVDVEZUPZXUEXUJTXUTVUIWVAWWPWJVKZVUIWXTWYCXVAWYAWYEVVLWXFYOVOZVUIXUMX
        VBXUQVUIXUOXVBXUPXUCVPUTWMZXVEVUIXUNXVDXURWWQUWGVKZWVAWXGXUCXJYPXAVUIXU
        TXVAXVEXVCXUGXULTXVFXVGXVIXVHWVAWXGXJXUCYPXAWOVUIVUOXUDWXHSVUIVYNVYOVYP
        VUOXUDTVYNVUIVYRVKVYSVYTUMHUWHVMZWIVUIWXNXUFWVBSVUIWXNWXGHHSQZRQZXUFVUI
        WXTWYCVYQVYQWXNXVLTWYAWYEWUAWUAVVLWXFHHYPXAVUIXUCXVKWXGRVUIVYOXUCXVKTVY
        SHUWIUTZWBWFWBWOWOVUIWVIWXHVUOSUBUBVQVDGUHQHUIUCUKZSQUWJQPUNGXVNXKQUJQS
        QUBUKXVNUJQSQUCUOXQZAVUMWVKUMSQHRQULUEVUSUJQXQZUCUAAGHNXVOYSXVPYSUWLZWB
        WFVUIVUMVUTVUOUAVVEVXJVVGVUTVWKVRZUWMYQVUIVVDGUSAUDWVKVUSUJQZXQZPUWNZUE
        VUKVUIVVCUAXVTPGVVGVWOVUPXVTUEVVCTVWPAVUPXVSVVCUDXVTWVKVUPVUSUJUWOXVTYS
        VUPVUSUJXTYGUTVUIGUDPUWPUEVUIUWQUWRUWSVYKUWTGBXWAJUXAUXBZUXCVUIVVDVUMVU
        OVUQUXDUEZVUSUJQZSQZUAUOZVUKVULUFVUIVUMVVCXWEUAVVEVWSVVGVVHXWDUQOZXWEUQ
        OVVRVVGXWCUQOZXWCVDVEZVWAXWGVVGWUFXWHWUIVUQUXLUTZVVGXWHVDXWCWPUGZXWIXWJ
        VVGXWKVDVUQUXEUEZWPUGZVVGVWDXWKXWMUPVWEVUQUXFUTZWQZXWCUXGVOVWJXWCVUSVLV
        MZVUOXWDVNVOVVGVVCVUOXWCUIUJQZRQZXWEUFVVGXWQVXPUFUGZVVCXWRUFUGZVVGXWQVX
        SVXPUFVVGXWCVUQUFUGZXWQVXSUFUGZVVGXWCVUQWPUGZXXAVVGWUJXXCWUMVUQUXHUTVVG
        XWHWUFXXCXXAXDXWJWUIXWCVUQXEVOXFVVGXWHVDXWCUFUGZWUFWUGXXAXXBXCXWJVVGXWK
        XXDXWOVVGVDUQOXWHXWKXXDXDUXIXWJVDXWCXEVAXFWUIWUNXWCVUQUXJXAXBWUBUXKVVGV
        VCXWQSQVUOUFUGZXWSXWTVVGXWQUQOZVVHVWNWUPXXEXWSXCVVGXWQVHOZXXFVVGXWCVHOZ
        VWHXXGVVGXWHXWKXXHXWJXWOXWCWRWSVJXWCUIWDWAZXWQVGUTZVVRVWSWUTXWQVUOVVCUX
        MYRVVGVWNVVHXXFVDXWQWPUGZXXEXWTXCVWSVVRXXJVVGXXGXXKXXIXWQXHUTVVCVUOXWQU
        XNYRUXOXBVVGXWEVUOPXWQRQZSQZXWRVVGXWDXXLVUOSVVGXWCVQOZVXDXWDXXLTVVGXWCX
        WJVRZVTXWCUIVSWAZWBVVGVXFXWQVQOZXWQVDVEZXWRXXMTVXKVVGXWQXXJVRZVVGXXGXXR
        XXIXWQVIUTZVUOXWQWEVMWFUXKXIXWBVUIVULWVEPWVCUSQZSQZVUOVUMXWDUAUOZSQZXWF
        AGWVPPWVNUSQZSQZXYBUDFWVTWVPWVEXYEXYASWWBWVTWVNWVCPUSWWAWBXMFDWWCCWWGQZ
        WWEQZAUDXYFXQZMXYHXYITXRAUDWVPXYESDXYGXSXSXSWWKWWMXYEXSOWWLPWVNUSXTVKWX
        CXRAUDPWVNUSWWCCXSVFXSWWKWWSWWTWXAWXBYCYCYEYFWVEXYASXTYGVUIWXKHPUSQZHRQ
        ZSQZWVBWXJXYKSQZSQZXYBXYDVUIWXPWXQXYKVQOZXYLXYNTWXSWYBVUIXYJVQOZVYOVYPX
        YOVUIVYOXYPVYSHUXPUTZVYSVYTXYJHYIVMWVBWXJXYKYJVMVUIWVEWXKXYAXYKSWYMVUIX
        YKWYHWVCUSQZXYAVUIVYOWYDVYOVYPXYKXYRTVYSWYJVYSVYTHPHYNYRVUIWYHPWVCUSWYL
        WIUXQXMVUIXUDVVLXYJSQZXJRQZSQZWVBXYSXUCRQZSQZXYDXYNVUIWVAXYSSQZXUIRQZYU
        DXUKRQZYUAYUCVUIXUIXUKYUDRXUSWBVUIXUTXYSVQOZXVCXVEYUAYUETXVFVUIWXTXYPYU
        GWYAXYQVVLXYJYOVOZXVHXVIWVAXYSXUCXJYPXAVUIXUTYUGXVEXVCYUCYUFTXVFYUHXVIX
        VHWVAXYSXJXUCYPXAWOVUIVUOXUDXYCXYTSXVJVUIXYCWXHGUSQZXYTVUIXYCVUMVUTPUSQ
        ZUAUOWVIVUMPUAUOZUSQYUIVUIVUMXWDYUJUAVVGXWDXXLPVUTUSQZYUJXXPVVGXWQXWLUI
        UJQZUSQZXWQRQZXWQXWQRQZYUMXWQRQZUSQZXXLYULVVGXXQYUMVQOZXXQXXRYUOYURTXXS
        VVGXWLVQOZYUSVVGXWLVVGWUFXWLUQOZWUIVUQUXRUTZVRZXWLWCUTZXXSXXTXWQYUMXWQY
        NYRVVGYUNPXWQRVVGVUQVQOZYUNPTVVGVUQWUIVRZVUQUYAUTWIVVGYUPPYUQVUTUSVVGXX
        QXXRYUPPTXXSXXTXWQYMVOVVGVUTVXBPXWQYUMRQZRQZYUQVXEVVGVWTYVGPRVVGVWTXWCX
        WLRQZUIUJQZYVGVVGVURYVIUIUJVVGYVEXWLVDVEZVURYVITYVFVVGYVAXWMYVKYVBVVGXW
        KXWMXWNUXSXWLUXGVOZVUQUXTVOWIVVGXXNYUTYVKYVJYVGTXXOYVCYVLXWCXWLUWHVMWNW
        BVVGXXQXXRYUSYUMVDVEZYVHYUQTXXSXXTYVDVVGYVMYVKYVLVVGYUTYVMYVKXCYVCXWLUY
        BUTUYCXWQYUMUYDXAUYEXMUYFVVGWYDVUTVQOYULYUJTYHXVRPVUTUYKVAYQUYGVUIVUMVU
        TPUAVVEXVRWYDVVGYHVKUYHVUIWVIWXHYUKGUSXVQVUIYUKVUMUYIUEZPSQZGPSQZGVUIVU
        MUYJOWYDYUKYVOTVVEYHVUMPUAUYLWAVUIYVNGPSVUIGWKOYVNGTGUYMGUYNUTWIVUIGVQO
        ZYVPGTGWHZGUYOUTYQXMYQVUIXYTWXGXJGSQZUSQZXJRQZWXHYVSXJRQZUSQZYUIVUIXYSY
        VTXJRVUIVVLWXFYTUSQZSQZWXGVVLYTSQZUSQZXYSYVTVUIWXTWYCYTVQOZYWEYWGTWYAWY
        EYWHVUIVUAVKZVVLWXFYTUYPVMVUIYWDXYJVVLSVUIYWDVVMPUSQZXYJVUIVVLYTPXKQZUS
        QZVVLXUAUSQZYWDYWJYWKXUAVVLUSYWKUIPUSQZPXKQZUIXUAYTYWNPXKUYQYKWYTWYDYWO
        UITWLYHUIPYLVUBUWKUYRWGVUIWXTWYDYWHYWDYWLTWYAWYJYWIVVLPYTUYSVMVUIWXTWYD
        WYDYWJYWMTWYAWYJWYJVVLPPUYTVMVUCHVVMPUSNYKXLWBVUIYWFYVSWXGUSVUIYWFUIYTS
        QZGSQZYVSVUIWYTYVQYWHYWFYWQTWYTVUIWLVKYVRYWIUIGYTVUDVMXJYWPGSYTUISQXJYW
        PVUEYTUIVUAWLVUFVUGYKXLWBUYFWIVUIXVAYVSVQOZXUNXVDYWAYWCTXVGVUIXUNYVQYWR
        XURYVRXJGYOVAXUNVUIXURVKZXVDVUIWWQVKZWXGYVSXJYNYRVUIYWBGWXHUSVUIYVQXUNX
        VDYWBGTYVRYWSYWTGXJVUHVMWBYQWFXMVUIXYMYUBWVBSVUIXYMXYSXVKRQZYUBVUIWXTXY
        PVYQVYQXYMYXATWYAXYQWUAWUAVVLXYJHHYPXAVUIXUCXVKXYSRXVMWBWFWBWOWOVUIVUMX
        WDVUOUAVVEVXJVVGXWDXWPVRUWMYQUXCWM $.
        $( [29-Jul-2014] $)
    $}

    $( Lemma for ~ basel .  Since by ~ basellem8 ` F ` is bounded by two
       expressions that tend to ` pi ^ 2 / 6 ` , ` F ` must also go to
       ` pi ^ 2 / 6 ` by the squeeze theorem ~ climsqueeze .  But the series
       ` F ` is exactly the partial sums of ` k ^ -u 2 ` , so it follows that
       this is also the value of the infinite sum
       ` sum_ k e. NN ( k ^ -u 2 ) ` . $)
    basellem9 $p |- sum_ k e. NN ( k ^ -u 2 ) = ( ( pi ^ 2 ) / 6 ) $=
      ( cn co wtru c1 cvv wcel cr cmul vx vy vz cv c2 cneg cexp csu cpi c6 cdiv
      wceq cmpt nnuz cz 1z a1i cfv oveq1 eqid ovex fvmpt adantl wa cc0 wne nnre
      wf nnne0 znegcl ax-mp reexpclz syl3anc fmptd ffvelrn sylan eqeltrrd recnd
      2z caddc cseq cli cmin cof serfre feq1i sylibr csn cxp remulcl wss fconst
      cc pire resqcli 6re 6nn nnne0i redivcli snssd fss sylancr resubcl 1re 2nn
      elexi nnex off syl2anc wfn ffn dffn5v sylib offval2 3eqtr4d recn c3 recni
      syl wbr climconst2 ax-resscn sylancl ax-1cn basellem7 fnconstg offn eqidd
      ofval climmul syl6breq syl5eqbr 3re 3cn mul01i 2cn ofc1 eqbrtrrd cle wb
      nnmulcl peano2nn nnrecre 3syl inidm readdcl npcan mpteq2dva 0re w3a subdi
      zrei syl3an caofdi oveq12i syl6eqr cuz eqimss2i ofnegsub negcli syl6eqbrr
      mulid1i basellem6 mulid2 mulneg1 negeqd mulcl negneg eqtr2d oveq12d eqtrd
      negsub df-3 addcomi oveq1i adddir syl5eq pnpcan 3eqtr4rd basellem8 simprd
      lesub1 3brtr4d simpld subge0 mpbird breqtrrd climsqueeze2 climadd addid2i
      eqtri mpbid 3brtr3g isumclim trud ) MAUDZUEUFZUGNZAUHUIUEUGNZUJUKNZULOUWR
      UWTABMBUDZUWQUGNZUMZPQMUNPUORZOUPUQZUWPMRZUWPUXCURZUWRULOBUWPUXBUWRMUXCUX
      AUWPUWQUGUSUXCUTZUWPUWQUGVAVBVCZOUXFVDZUWRUXJUXGUWRSUXIOMSUXCVHUXFUXGSROB
      MUXBSUXCUXAMRZUXBSRZOUXKUXASRUXAVEVFUWQUORZUXLUXAVGUXAVIUXMUXKUEUORUXMVSU
      EVJVKZUQUXAUWQVLVMVCUXHVNMSUWPUXCVOVPZVQVRUWTQROUWSUJUKVAZUQZOCVEUWTVTNZV
      TUXCPWAZUWTWBOCFWCWDZNZFVTWDZNZCUXRWBOBMUXACURZUXAFURZWCNZUYEVTNZUMBMUYDU
      MZUYCCOBMUYGUYDOUXKVDZUYDWMRUYEWMRUYGUYDULUYIUYDOMSCVHZUXKUYDSROMSUXSVHUY
      JOAUXCPMUNUXEUXOWEMSCUXSIWFWGZMSUXACVOVPZVRUYIUYEOMSFVHZUXKUYESROMSEMPWHZ
      WIZMUWQWHZWIZDTWDZNZUYBNZUYRNZVHUYMOUAUBMMMTSSSEUYTQQUAUDZSRZUBUDZSRZVDZV
      UBVUDTNZSROVUBVUDWJVCZOMSMUWTWHZWIZUYODUXTNZUYRNZVHMSEVHZOUAUBMMMTSSSVUJV
      UKQQVUHOMVUIVUJVHVUISWKMSVUJVHZMUWTUXPWLOUWTSUWTSROUWSUJUIWNWOWPUJWQWRWSZ
      UQZWTMVUISVUJXAXBZOUAUBMMMWCSSSUYODQQVUFVUBVUDWCNSROVUBVUDXCVCZOMUYNUYOVH
      UYNSWKMSUYOVHZMPPUOUPXFWLOPSPSROXDUQZWTMUYNSUYOXAXBZOBMPUEUXATNZPVTNZUKNZ
      SDUYIVVBMRZVVCMRVVDSROUEMRZUXKVVEVVFOXEUQUEUXAUUAVPVVBUUBVVCUUCUUDHVNZMQR
      ZOXGUQZVVIMUUEZXHZVVIVVIVVJXHMSEVULJWFWGZOUAUBMMMVTSSSUYOUYSQQVUFVUBVUDVT
      NSROVUBVUDUUFVCZVVAOUAUBMMMTSSSUYQDQQVUHOMUYPUYQVHUYPSWKMSUYQVHMUWQUWQUOU
      XNXFWLOUWQSUWQSRZOUWQUXNUULZUQZWTMUYPSUYQXAXBVVGVVIVVIVVJXHVVIVVIVVJXHZVV
      IVVIVVJXHMSFVUAKWFWGZMSUXAFVOVPZVRUYDUYEUUGXIUUHOBMUYFUYEVTUYAFQQSVVIUYFQ
      RUYIUYDUYEWCVAUQVVSOBMUYDUYEWCCFQSSVVIUYLVVSOCMXJZCUYHULOUYJVVTUYKMSCXKXS
      ZBMCXLXMZOFMXJZFBMUYEUMULOUYMVWCVVRMSFXKXSZBMFXLXMZXNVWEXNVWBXOOVEUWTAUYA
      FUYCPSQQMUNUXEVESROUUIUQZOVEAGFUXTNZUYAPSQMUNUXEVWFOEUYODUYBNZUYTUXTNZUYR
      NZVWGVEWBOVWJEVWHUYRNZVUAUXTNVWGOUAUBUCMWCSTEVWHUYTQVVIVVLOUAUBMMMVTSSSUY
      ODQQVVMVVAVVGVVIVVIVVJXHZVVQVUCVUEUCUDZSRZUUJVUBVUDVWMWCNTNVUGVUBVWMTNWCN
      ULZOVUCVUBWMRVUEVUDWMRVWNVWMWMRVWOVUBXPVUDXPVWMXPVUBVUDVWMUUKUUMVCUUNGVWK
      FVUAUXTLKUUOUUPOVWJUWTVETNVEWBOUWTVEAEMXQWHZWIZDUYRNZVWJPSSQMUNUXEVUPOEVU
      LUWTWBJOVULUWTPTNZUWTWBOUWTPAVUJVUKVULPQSQMUNUXEUXQOUWTWMRUXDVUJUWTWBXTUW
      TVUOXRZUXEUWTPMMPUUQURUNUURZXGYAXBVUTVULQROVUJVUKUYRVAUQOVUKUYOMPUFZWHWID
      UYRNUYBNZPWBOVVHMWMUYOVHZMWMDVHZVXCVUKULVVIOVUSSWMWKZVXDVVAYBMSWMUYOXAYCO
      MSDVHZVXFVXEVVGYBMSWMDXAYCMUYODQUUSVMVXBBDHPYDUUTYEUVAUXJUWPVUJURZOVUNUXF
      VXHSRVUQMSUWPVUJVOVPVRUXJUWPVUKURZOMSVUKVHUXFVXISRVVKMSUWPVUKVOVPVROMMVXH
      VXITMVUJVUKQQUWPOVUNVUJMXJVUQMSVUJXKXSOMMWCMUYODQQOUXDUYOMXJUXEMPUOYFXSZO
      VXGDMXJVVGMSDXKXSZVVIVVIVVJYGVVIVVIVVJUXJVXHYHUXJVXIYHYIYJUWTVWTUVBZYKYLZ
      VWFVWJQROEVWIUYRVAUQOVWRXQVETNVEWBOXQVEAVWQDVWRPSSQMUNUXEXQSROYMUQZOXQWMR
      UXDVWQXQWBXTYNUXEXQPMVXAXGYAXBVWFVWRQROVWQDUYRVAUQDVEWBXTOBDHUVCUQUXJUWPV
      WQURZOMSVWQVHZUXFVXOSROMVWPVWQVHVWPSWKVXPMXQXQSYMXFWLOXQSVXNWTMVWPSVWQXAX
      BZMSUWPVWQVOVPVRUXJUWPDURZOVXGUXFVXRSRZVVGMSUWPDVOVPZVRZOMMVXOVXRTMVWQDQQ
      UWPOVXPVWQMXJVXQMSVWQXKXSVXKVVIVVIVVJUXJVXOYHUXJVXRYHZYIYJXQYNYOYKUXJUWPE
      URZOVUMUXFVYCSRVVLMSUWPEVOVPVRZUXJUWPVWRURZOMSVWRVHUXFVYESROUAUBMMMTSSSVW
      QDQQVUHVXQVVGVVIVVIVVJXHMSUWPVWRVOVPVROMMVYCVYETMEVWIQQUWPOVUMEMXJVVLMSEX
      KXSZOMSVWIVHVWIMXJOUAUBMMMWCSSSVWHUYTQQVURVWLVVQVVIVVIVVJXHMSVWIXKXSVVIVV
      IVVJUXJVYCYHZUXJPVXRVTNZPUWQVXRTNZVTNZWCNZXQVXRTNZUWPVWIURVYEUXJPVXRTNZUE
      VXRTNZVTNZVXRVYIWCNZVYLVYKUXJVYOVXRVYIUFZVTNZVYPUXJVYMVXRVYNVYQVTUXJVXRWM
      RZVYMVXRULVYAVXRUVDXSUXJVYQVYNUFZUFZVYNUXJVYIVYTUXJUEWMRZVYSVYIVYTULYPVYA
      UEVXRUVEXBUVFUXJVYNWMRZWUAVYNULUXJWUBVYSWUCYPVYAUEVXRUVGXBVYNUVHXSUVIUVJU
      XJVYSVYIWMRZVYRVYPULVYAUXJVYIUXJVVNVXSVYISRVVOVXTUWQVXRWJXBVRZVXRVYIUVLXI
      UVKUXJVYLPUEVTNZVXRTNZVYOXQWUFVXRTXQUEPVTNWUFUVMUEPYPYDUVNUWKUVOUXJPWMRZW
      UBVYSWUGVYOULWUHUXJYDUQZWUBUXJYPUQVYAPUEVXRUVPVMUVQUXJWUHVYSWUDVYKVYPULWU
      IVYAWUEPVXRVYIUVRVMUVSOMMVYHVYJWCMVWHUYTQQUWPOMMVTMUYODQQVXJVXKVVIVVIVVJY
      GOMMVTMUYOUYSQQVXJOMMTMUYQDQQOUXMUYQMXJUXMOUXNUQMUWQUOYFXSVXKVVIVVIVVJYGZ
      VVIVVIVVJYGZVVIVVIVVJOMPVXRVTDQSUWPVVIVUTVXKVYBYQOMPVYIVTUYSQSUWPVVIVUTWU
      JOMUWQVXRTDQSUWPVVIVVPVXKVYBYQYQYIOMXQVXRTDQSUWPVVIVXNVXKVYBYQXOYIYJUWTVW
      TYOYKYRUYAQROCFUXTVAUQOMSVWGVHUXFUWPVWGURZSROUAUBMMMWCSSSGFQQVUROMSVWKVHM
      SGVHZOUAUBMMMTSSSEVWHQQVUHVVLVWLVVIVVIVVJXHMSGVWKLWFWGZVVRVVIVVIVVJXHMSUW
      PVWGVOVPOMSUYAVHZUXFUWPUYAURZSROUAUBMMMWCSSSCFQQVURUYKVVRVVIVVIVVJXHZMSUW
      PUYAVOVPZUXJUWPCURZUWPFURZWCNZUWPGURZWUTWCNZWUPWULYSUXJWUSWVBYSXTZWVAWVCY
      SXTZUXJWUTWUSYSXTZWVDUXFWVFWVDVDOBCDEFGUWPUEUWPTNPVTNZHIJKLWVGUTUVTVCZUWA
      UXJWUSSRZWVBSRZWUTSRZWVDWVEYTOUYJUXFWVIUYKMSUWPCVOVPZOWUMUXFWVJWUNMSUWPGV
      OVPOUYMUXFWVKVVRMSUWPFVOVPZWUSWVBWUTUWBVMUWLOMMWUSWUTWCMCFQQUWPVWAVWDVVIV
      VIVVJUXJWUSYHUXJWUTYHZYIZOMMWVBWUTWCMGFQQUWPOWUMGMXJWUNMSGXKXSVWDVVIVVIVV
      JUXJWVBYHWVNYIUWCUXJVEWVAWUPYSUXJVEWVAYSXTZWVFUXJWVFWVDWVHUWDUXJWVIWVKWVP
      WVFYTWVLWVMWUSWUTUWEXIUWFWVOUWGUWHUXQUYCQROUYAFUYBVAUQOFVUAUWTWBKOVUAVWSU
      WTWBOUWTPAEUYTVUAPQSQMUNUXEUXQVXMVUTVUAQROEUYTUYRVAUQUYTPWBXTOUWQBDHUWQVV
      OXRYEUQVYDUXJUWPUYTURZOMSUYTVHUXFWVQSRVVQMSUWPUYTVOVPVROMMVYCWVQTMEUYTQQU
      WPVYFWUKVVIVVIVVJVYGUXJWVQYHYIYJVXLYKYLUXJWUPWURVRUXJWUTWVMVROMMWUPWUTVTM
      UYAFQQUWPOWUOUYAMXJWUQMSUYAXKXSVWDVVIVVIVVJUXJWUPYHWVNYIUWIYRIUWTVWTUWJUW
      MUWNUWO $.
      $( [28-Jul-2014] $)
  $}

  ${
    $d k m n $.
    $( The sum of the inverse squares is ` pi ^ 2 / 6 ` .  This is commonly
       known as the Basel problem, with the first known proof attributed to
       Euler.  See ~ http://en.wikipedia.org/wiki/Basel_problem .  This
       particular proof approach is due to Cauchy (1821).  (Contributed by
       Mario Carneiro, 30-Jul-2014.) $)
    basel $p |- sum_ k e. NN ( k ^ -u 2 ) = ( ( pi ^ 2 ) / 6 ) $=
      ( vn vm caddc cn cv c2 cexp co cmpt c1 cseq cmul cdiv csn cxp cof cbvmptv
      wceq eqid cneg cpi cmin oveq2 oveq1d oveq2d oveq1 seqeq3 ax-mp basellem9
      c6 ) ABDCECFZGUAZHIZJZKLZCEKGULMIZKDIZNIZJZEUBGHIUKNIOPEKOPZUTUCQIMQZIZVC
      VAEUMOPUTVBIDQZIVBIZVCVAUTVDIVBIZCBEUSKGBFZMIZKDIZNIULVGSZURVIKNVJUQVHKDU
      LVGGMUDUEUFRUOBEVGUMHIZJZSUPDVLKLSCBEUNVKULVGUMHUGRDUOVLKUHUIVCTVETVFTUJ
      $.
      $( [30-Jul-2014] $)
  $}

