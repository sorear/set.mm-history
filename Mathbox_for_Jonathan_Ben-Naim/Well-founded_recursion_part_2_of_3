$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Well-founded recursion, part 2 of 3
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A d f x $.  $d B f $.  $d G d f x $.  $d R d f x $.  $d Y d $.
    bnj1500.1 $e |- B = { d | ( d C_ A /\ A. x e. d _pred ( x , A , R ) C_ d )
        } $.
    bnj1500.2 $e |- Y = <. x , ( f |` _pred ( x , A , R ) ) >. $.
    bnj1500.3 $e |- C = { f | E. d e. B ( f Fn d /\ A. x e. d ( f ` x ) = ( G `
        Y ) ) } $.
    bnj1500.4 $e |- F = U. C $.
    $( Well-founded recursion, part 2 of 3.  The proof has been taken from
       Chapter 4 of Don Monk's notes on Set Theory.  See
       ~ http://euclid.colorado.edu/~~monkd/settheory/ . $)
    bnj1500 $p |- ( R _FrSe A -> A. x e. A ( F ` x ) = ( G ` <. x , ( F |`
        _pred ( x , A , R ) ) >. ) ) $=
      ( syn-bnj15 cv wcel wa cdm w3a wceq biid bnj1501 ) BEOAPZBQRZUEFPZDQUDUFS
      ZQTZUHJPZCQUGUIUATZABCDEFGHIJKLMNUEUBUHUBUJUBUC $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B w $.  $d C w $.  $d F w $.  $d H w x $.  $d R x $.
    $d d w x $.  $d f x $.
    bnj1525.1 $e |- B = { d | ( d C_ A /\ A. x e. d _pred ( x , A , R ) C_ d )
        } $.
    bnj1525.2 $e |- Y = <. x , ( f |` _pred ( x , A , R ) ) >. $.
    bnj1525.3 $e |- C = { f | E. d e. B ( f Fn d /\ A. x e. d ( f ` x ) = ( G `
        Y ) ) } $.
    bnj1525.4 $e |- F = U. C $.
    bnj1525.5 $e |- ( ph <-> ( R _FrSe A /\ H Fn A /\ A. x e. A ( H ` x ) = ( G
        ` <. x , ( H |` _pred ( x , A , R ) ) >. ) ) ) $.
    bnj1525.6 $e |- ( ps <-> ( ph /\ F =/= H ) ) $.
    $( Technical lemma of ~ bnj1522 .  This lemma may no longer be used or have
       become an indirect lemma of the theorem in question (i.e. a lemma of a
       lemma... of the theorem). $)
    bnj1525 $p |- ( ps -> A. x ps ) $=
      ( vw wne wa syn-bnj15 wfn cv cfv syn-bnj14 cres cop wceq wral w3a hbra1
      bnj1350 bnj572 cuni bnj1309 bnj1307 hbuni hbxfr wcel ax-17 hbne hban ) BA
      IKUAZUBCSAVECADGUCZKDUDZCUEZKUFVHKDGVHUGUHUIJUFUJZCDUKZULCRVFVGVJCVICDUMU
      NUOCTIKCTIFUPQCTFCTEFHJLMPCTDEGMNUQURUSUTTUEKVACVBVCVDUO $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d R x y $.
    bnj1528.1 $e |- ( ph -> R _FrSe A ) $.
    bnj1528.2 $e |- ( ph -> B C_ A ) $.
    bnj1528.3 $e |- ( ph -> B =/= (/) ) $.
    $( Existence of a minimal element in certain classes: if ` R ` is
       well-founded and set-like on ` A ` , then every non-empty subclass of
       ` A ` has a minimal element.  The proof has been taken from Chapter 4 of
       Don Monk's notes on Set Theory.  See
       ~ http://euclid.colorado.edu/~~monkd/settheory/ . $)
    bnj1528 $p |- ( ph -> E. x e. B A. y e. B -. y R x ) $=
      ( syn-bnj15 wss c0 wne cv wbr wn wral wrex bnj69 syl3ancOLD ) DFJEDKELMCNBNF
      OPCEQBERABCDEFSGHIT $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A w x y $.  $d F w y $.  $d G w x y $.  $d R w x y $.
    bnj1529.1 $e |- ( ch -> A. x e. A ( F ` x ) = ( G ` <. x , ( F |` _pred ( x
        , A , R ) ) >. ) ) $.
    bnj1529.2 $e |- ( w e. F -> A. x w e. F ) $.
    $( Technical lemma of ~ bnj1522 .  This lemma may no longer be used or have
       become an indirect lemma of the theorem in question (i.e. a lemma of a
       lemma... of the theorem). $)
    bnj1529 $p |- ( ch -> A. y e. A ( F ` y ) = ( G ` <. y , ( F |` _pred ( y ,
        A , R ) ) >. ) ) $=
      ( cv cfv syn-bnj14 cres cop wceq wral ax-17 wcel hbfv hbres hbop hbeq
      fveq2 id bnj602 bnj1324 opeq12d fveq2d eqeq12d cbvral sylib ) ABKZGLZUMGE
      FUMMZNZOZHLZPZBEQCKZGLZUTGEFUTMZNZOZHLZPZCEQIUSVFBCEUSCRBDDVAVEBDUTGJDKZU
      TSBRZTBDVDHVGHSBRBDUTVCVHBDGVBJVGVBSBRUAUBTUCUMUTPZUNVAURVEUMUTGUDVIUQVDH
      VIUMUTUPVCVIUEVIUOVBGEFUMUTUFUGUHUIUJUKUL $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A w x y $.  $d G w x y $.  $d H w x y $.  $d R w x y $.
    bnj1530.1 $e |- ( ch -> A. x e. A ( H ` x ) = ( G ` <. x , ( H |` _pred ( x
        , A , R ) ) >. ) ) $.
    $( Technical lemma of ~ bnj1522 .  This lemma may no longer be used or have
       become an indirect lemma of the theorem in question (i.e. a lemma of a
       lemma... of the theorem). $)
    bnj1530 $p |- ( ch -> A. y e. A ( H ` y ) = ( G ` <. y , ( H |` _pred ( y ,
        A , R ) ) >. ) ) $=
      ( vw cv cfv syn-bnj14 cres cop wceq wral ax-17 wcel hbfv hbres hbop hbeq
      fveq2 id bnj602 bnj1324 opeq12d fveq2d eqeq12d cbvral sylib ) ABJZGKZULGD
      EULLZMZNZFKZOZBDPCJZGKZUSGDEUSLZMZNZFKZOZCDPHURVEBCDURCQBIIUTVDBIUSGIJZGR
      BQZVFUSRBQZSBIVCFVFFRBQBIUSVBVHBIGVAVGVFVARBQTUASUBULUSOZUMUTUQVDULUSGUCV
      IUPVCFVIULUSUOVBVIUDVIUNVAGDEULUSUEUFUGUHUIUJUK $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B w $.  $d C w $.  $d d w x $.  $d f x $.
    bnj1535.1 $e |- B = { d | ( d C_ A /\ A. x e. d _pred ( x , A , R ) C_ d )
        } $.
    bnj1535.2 $e |- Y = <. x , ( f |` _pred ( x , A , R ) ) >. $.
    bnj1535.3 $e |- C = { f | E. d e. B ( f Fn d /\ A. x e. d ( f ` x ) = ( G `
        Y ) ) } $.
    bnj1535.4 $e |- F = U. C $.
    $( Technical lemma of ~ bnj1522 .  This lemma may no longer be used or have
       become an indirect lemma of the theorem in question (i.e. a lemma of a
       lemma... of the theorem). $)
    bnj1535 $p |- ( w e. F -> A. x w e. F ) $=
      ( cuni bnj1309 bnj1307 hbuni hbxfr ) ABHEPOABEABDEGIJKNABCDFKLQRST $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d A d f x $.  $d A d w x $.  $d A w x y z $.  $d B f $.  $d B w $.
    $d C w $.  $d D y z $.  $d F w y z $.  $d G d f x $.  $d G d w x $.
    $d G w x y $.  $d H w x y z $.  $d R d f x $.  $d R d w x $.
    $d R w x y z $.  $d Y d $.  $d ch y $.
    bnj1523.1 $e |- B = { d | ( d C_ A /\ A. x e. d _pred ( x , A , R ) C_ d )
        } $.
    bnj1523.2 $e |- Y = <. x , ( f |` _pred ( x , A , R ) ) >. $.
    bnj1523.3 $e |- C = { f | E. d e. B ( f Fn d /\ A. x e. d ( f ` x ) = ( G `
        Y ) ) } $.
    bnj1523.4 $e |- F = U. C $.
    bnj1523.5 $e |- ( ph <-> ( R _FrSe A /\ H Fn A /\ A. x e. A ( H ` x ) = ( G
        ` <. x , ( H |` _pred ( x , A , R ) ) >. ) ) ) $.
    bnj1523.6 $e |- ( ps <-> ( ph /\ F =/= H ) ) $.
    bnj1523.7 $e |- ( ch <-> ( ps /\ x e. A /\ ( F ` x ) =/= ( H ` x ) ) ) $.
    bnj1523.8 $e |- D = { x e. A | ( F ` x ) =/= ( H ` x ) } $.
    bnj1523.9 $e |- ( th <-> ( ch /\ y e. D /\ A. z e. D -. z R y ) ) $.
    $( Technical lemma of ~ bnj1522 .  This lemma may no longer be used or have
       become an indirect lemma of the theorem in question (i.e. a lemma of a
       lemma... of the theorem). $)
    bnj1523 $p |- ( ( R _FrSe A /\ H Fn A /\ A. x e. A ( H ` x ) = ( G ` <. x ,
        ( H |` _pred ( x , A , R ) ) >. ) ) -> F = H ) $=
      ( vw syn-bnj15 wfn cv cfv syn-bnj14 cres cop wceq wral w3a wne bnj60
      bnj835 bnj832 3simp2bi pm3.27bi bnj1535 bnj1542 bnj1525 bnj1521 wbr wn
      wcel 3simp1bi wss bnj21 a1i 3simp3bi bnj1526 bnj1527 bnj1528 bnj1209
      bnj213 bnj1531 bnj1418 bnj1532 bnj1534 bnj1533 bnj1536 bnj1537 bnj1500
      bnj1529 bnj1213 bnj1294 bnj1530 3eqtr4d bnj1538 bnj836 bnj1539 bnj1419
      bnj1541 sylbir ) HLUIZPHUJZEUKZPULZXCPHLXCUMZUNUOOULUPEHUQZURANPUPUCBANPU
      DBCEXCNULZXDUSZBCEHBEUHHNPANPUSZNHUJZBUDXAXBXFXJAUCEHIJLMNOQRSTUAUBUTVAVB
      ZAXIXBBUDAXAXBXFUCVCVBZBAXIUDVDEUHHIJLMNOQRSTUAUBVEZVFUEABEHIJLMNOPQRSTUA
      UBUCUDVGVHCDFGUKZFUKZLVIZVJGKUQZCDFKCFGHKLBXCHVKZXHXACUEAXIXABUDAXAXBXFUC
      VLVBVAKHVMCXHEHKUFVNZVOCXCKXHCEKHUFCBXRXHUEVCCBXRXHUEVPVQVRVSUGVTDXONULZX
      OPULZDXONHLXOUMZUNZUOOULZXOPYBUNZUOOULZXTYADYCYEXOODGHYBNPCXOKVKZXQXJDUGB
      XRXHXJCUEXKVAVACYGXQXBDUGBXRXHXBCUEXLVAVAYBHVMDHLXOWAZVODGHYBXNNULKXNPULX
      PXNKVKVJDGYBDXPGKDCYGXQUGVPWBFGHLWCWDYHEGUHHKNPUFXMWEWFWGWHDXTYDUPZFHCYGX
      QYIFHUQDUGCEFUHHLNOBXRXHXGXCNXEUNUOOULUPEHUQZCUEAXIYJBUDXAXBXFYJAUCEHIJLM
      NOQRSTUAUBWIVAVBVAXMWJVADFKHXSDCYGXQUGVCWKZWLDYAYFUPZFHCYGXQYLFHUQDUGCEFH
      LOPBXRXHXFCUEAXIXFBUDAXAXBXFUCVPVBVAWMVAYKWLWNCYGXQXTYAUSZDUGYMFKHEFUHHKN
      PUFXMWEWOWPWQWRWRWSWT $.
      $( [3-Jun-2011] $)
  $}

