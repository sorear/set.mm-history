$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                Mathbox for Stefan Allan
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  ${
    foo3.1 $e |- ph $.
    $( A theorem about the universal class.  (Contributed by Stefan Allan,
       9-Dec-2008.) $)
    foo3 $p |- _V = { x | ph } $=
      ( cvv cv wceq cab df-v equid 2th abbii eqtri ) DBEZMFZBGABGBHNABNABICJKL
      $.
      $( [9-Dec-2008] $)
  $}

  $( A partial converse to ~ 19.9t .  (Contributed by Stefan Allan,
     21-Dec-2008.) $)
  xfree $p |- ( A. x ( ph -> A. x ph ) <-> A. x ( E. x ph -> ph ) ) $=
    ( wal wi wex 19.9t a5i 19.8a hbe1 stdpc5 syl5 impbii ) AABCZDZBCABEZADZBCZN
    PBABFGPNBAOQMABHOABABIJKGL $.
    $( [21-Dec-2008] $)

  $( A partial converse to ~ 19.9t .  (Contributed by Stefan Allan,
     21-Dec-2008.) $)
  xfree2 $p |- ( A. x ( ph -> A. x ph ) <-> A. x ( -. ph -> A. x -. ph ) ) $=
    ( wal wi wex wn xfree df-ex imbi1i con1b bitri albii ) AABCDBCABEZADZBCAFZO
    BCZDZBCABGNQBNPFZADQMRAABHIPAJKLK $.
    $( [21-Dec-2008] $)

  $( A proof readability experiment for ~ addltmul .  (Contributed by Stefan
     Allan, 30-Oct-2010.) $)
  addltmulALT $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 2 < A /\ 2 < B ) )
              -> ( A + B ) < ( A x. B ) ) $=
    ( cr wcel wa c2 clt wbr c1 cmin co caddc cmul a1i syl3anc ax-1cn syl adantr
    wb wceq simpr 2re simpl 1re ltsub1 df-2 eqcomi subaddrii breq1i bitrd mpbid
    2cn anim12i an4s wi peano2rem anim1i mulgt1 ex cc recn mulsub breq2d biimpd
    jca mulid2i eqcom biimpi opreq2d mulid1 adantl opreq12d ax-addrcl ax-mulrcl
    ax-mp readdcl syl2anc ltaddsub2 ltadd1 bicomd sylbird 3syld mpd ) ACDZBCDZE
    ZFAGHZFBGHZEZEZIAIJKZGHZIBIJKZGHZEZABLKZABMKZGHZWDWGWEWHWOWDWGEZWLWEWHEZWNW
    SWGWLWDWGUAWSWGFIJKZWKGHZWLWSFCDZWDICDZWGXBSXCWSUBNWDWGUCXDWSUDNFAIUEOXBWLS
    WSXAIWKGFIIULPPFIILKUFUGUHZUINUJUKWTWHWNWEWHUAWTWHXAWMGHZWNWTXCWEXDWHXFSXCW
    TUBNWEWHUCXDWTUDNFBIUEOXFWNSWTXAIWMGXEUINUJUKUMUNWJWOIWKWMMKZGHZIWQIIMKZLKZ
    AIMKZBIMKZLKZJKZGHZWRWFWOXHUOWIWFWOXHWFWOEWKCDZWMCDZEZWOEXHWFXRWOWDXPWEXQAU
    PBUPUMUQWKWMURQUSRWFXHXOUOWIWFXHXOWFXGXNIGWFAUTDZIUTDZEZBUTDZXTEZEXGXNTWDYA
    WEYCWDXSXTAVAZXTWDPNVEWEYBXTBVAZXTWEPNVEUMAIBIVBQVCVDRWFXOWRUOWIWFXOIWQILKZ
    WPJKZGHZWRWFYGXNIGWFYFXJWPXMJWFIXIWQLIXITZWFXIITZYIIPVFYJYIXIIVGVHVONVIWFAX
    KBXLLWDAXKTZWEWDXSYKYDXSXKATZYKAVJYLYKXKAVGVHQQRWEBXLTZWDWEXLBTZYMWEYBYNYEB
    VJQYNYMXLBVGVHQVKVLVLVCWFYHWPILKYFGHZWRWFWPCDZXDYFCDZYOYHSABVMZXDWFUDNZWFWQ
    CDZXDYQABVNZYSWQIVPVQWPIYFVROWFYOWRWFWRYOWFYPYTXDWRYOSYRUUAYSWPWQIVSOVTVDWA
    WARWBWC $.
    $( [30-Oct-2010] $)

$( (End of Stefan Allan's mathbox.) $)


