$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Introduce the Tarksi-Grothendieck Axiom
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f $.
    $( The Tarksi-Grothendieck Axiom.  For every set ` x ` there is an
       inaccessible cardinal ` y ` such that ` y ` is not in ` x ` .  The
       addition of this axiom to ZFC set theory provides a framework for
       category theory, thus for all practical purposes giving us a complete
       foundation for "all of mathematics."  This version of the axiom is used
       by the Mizar project
       ( ~ http://www.mizar.org/JFM/Axiomatics/tarski.html ).  Unlike the ZFC
       axioms, this axiom is very long when expressed in terms of primitive
       symbols - see ~ grothprim .  An open problem is finding a shorter
       equivalent. $)
    ax-groth $a |- E. y ( x e. y /\ A. z e. y ( A. w ( w C_ z -> w e. y ) /\
                       E. w e. y A. v ( v C_ z -> v e. w ) ) /\
                     A. z ( z C_ y -> ( z ~~ y \/ z e. y ) ) ) $.

    $( The Tarski-Grothendieck axiom using abbreviations. $)
    axgroth5 $p |- E. y ( x e. y /\ A. z e. y ( ~P z C_ y
             /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) $=
      ( vv cv wcel cpw wss wrex wa wral cen wbr wo w3a wex wi wal ax-groth
      biid pwss rexbii anbi12i ralbii df-ral visset elpw imbi1i albii bitri
      3anbi123i exbii mpbir ) AFBFZGZCFZHZUOIZURDFZIZDUOJZKZCUOLZUQUOMNUQUOGOZC
      UOHZLZPZBQUPUTUQIUTUOGRDSZEFZUQIVJUTGRESZDUOJZKZCUOLZUQUOIZVERZCSZPZBQABC
      DETVHVRBUPUPVDVNVGVQUPUAVCVMCUOUSVIVBVLDUQUOUBVAVKDUOEUQUTUBUCUDUEVGUQVFG
      ZVERZCSVQVECVFUFVTVPCVSVOVEUQUOCUGUHUIUJUKULUMUN $.
      $( [22-Jun-2009] $)

    $( Alternate version of the Tarksi-Grothendieck Axiom. $)
    axgroth2 $p |- E. y ( x e. y /\ A. z e. y ( A. w ( w C_ z -> w e. y ) /\
                       E. w e. y A. v ( v C_ z -> v e. w ) ) /\
                     A. z ( z C_ y -> ( y ~<_ z \/ z e. y ) ) ) $=
      ( cv wcel wss wi wal wrex wa wral cdom wbr wo w3a wex cen ax-groth cvv
      visset ssdomg ax-mp biantrurd wb sbthbg syl6bb orbi1d pm5.74i albii
      3anbi3i exbii mpbir ) AFBFZGZDFZCFZHUQUOGIDJEFZURHUSUQGIEJDUOKLCUOMZURUOH
      ZUOURNOZURUOGZPZIZCJZQZBRUPUTVAURUOSOZVCPZIZCJZQZBRABCDETVGVLBVFVKUPUTVEV
      JCVAVDVIVAVBVHVCVAVBURUONOZVBLZVHVAVMVBURUAGVAVMICUBURUOUAUCUDUEUOUAGVNVH
      UFBUBURUOUAUGUDUHUIUJUKULUMUN $.
      $( [18-Mar-2007] $)

    $( Derive the Axiom of Power Sets ~ ax-pow from the Tarksi-Grothendieck
       axiom ~ ax-groth .  That it follows is mentioned by Bob Solovay at
       ~ http://www.cs.nyu.edu/pipermail/fom/2008-March/012783.html .  Note
       that ~ ax-pow is not used by the proof.  (Contributed by G&eacute;rard
       Lang, 22-Jun-2009.) $)
    grothpw $p |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $=
      ( vv cv cpw cvv wcel wi wal wex wss wrex wa wral cen wbr wo w3a ax-groth
      biid dfss2 df-pw abeq2i imbi1i albii bitri rexbii anbi12i ralbii df-ral
      visset elpw 3anbi123i exbii mpbir simpl ralimi wceq pweq sseq1d rcla4cv
      syl anim2i 3adant3 pm3.35 ssex 3syl 19.23aiv ax-mp ssid elpwg mpbiri
      eleq2d cla4egv mpd elisset impbii elpw2 3bitri mpbi ) AFZGZHIZDFZCFZIWFWC
      IJDKZWGBFZIZJZCKZBLZWCWIIZWGGZWIMZWOWFMZDWINZOZCWIPZWGWIQRWJSZCWIGZPZTZBL
      ZWEXEWNWFWGMZWFWIIZJZDKZEFZWGMZXJWFIZJZEKZDWINZOZCWIPZWGWIMZXAJZCKZTZBLAB
      CDEUAXDYABWNWNWTXQXCXTWNUBWSXPCWIWPXIWRXOWPWFWOIZXGJZDKXIDWOWIUCYCXHDYBXF
      XGXFDWODWGUDUEUFUGUHWQXNDWIWQXJWOIZXLJZEKXNEWOWFUCYEXMEYDXKXLXKEWOEWGUDUE
      UFUGUHUIUJUKXCWGXBIZXAJZCKXTXACXBULYGXSCYFXRXAWGWICUMUNUFUGUHUOUPUQXDWEBX
      DWNWNWDWIMZJZOZYHWEWNWTYJXCWTYIWNWTWPCWIPYIWSWPCWIWPWRURUSWPYHCWCWIWGWCUT
      WOWDWIWGWCVAVBVCVDVEVFWNYHVGWDWIBUMZVHVIVJVKWEWDXBIZBLZWMWEYMWEWDWDGZIZYM
      WEYOWDWDMWDVLWDWDHVMVNYLYOBWDHWIWDUTXBYNWDWIWDVAVOVPVQYLWEBWDXBVRVJVSYLWL
      BYLYHWGWCMZWJJZCKZWLWDWIYKVTYHWGWDIZWJJZCKYRCWDWIUCYTYQCYSYPWJYPCWDCWCUDU
      EUFUGUHYQWKCYPWHWJDWGWCUCUFUGWAUPUHWB $.
      $( [22-Jun-2009] $)

    $( Derive the Axiom of Power Sets from the Tarksi-Grothendieck axiom
       ~ ax-groth .  Note that ~ ax-pow is not used by the proof.  Use ~ axpweq
       to obtain ~ ax-pow . $)
    grothpwex $p |- ~P x e. _V $=
      ( vy vz vw cv wcel cpw wss wrex wa wral cen wbr wo w3a wex cvv axgroth5
      wi simpl ralimi wceq pweq sseq1d rcla4cv syl anim2i 3adant3 pm3.35
      visset ssex 3syl 19.23aiv ax-mp ) AEZBEZFZCEZGZUPHZUSDEHDUPIZJZCUPKZURUPL
      MURUPFNCUPGKZOZBPUOGZQFZABCDRVEVGBVEUQUQVFUPHZSZJZVHVGUQVCVJVDVCVIUQVCUTC
      UPKVIVBUTCUPUTVATUAUTVHCUOUPURUOUBUSVFUPURUOUCUDUEUFUGUHUQVHUIVFUPBUJUKUL
      UMUN $.
      $( [16-Jan-2011] $) $( [22-Jun-2009] $)

    $( The Tarski-Grothendieck axiom using abbreviations.  This version is
       called Tarski's axiom: given a set ` x ` , there exists a set ` y `
       containing ` x ` , the subsets of the members of ` y ` , the power sets
       of the members of ` y ` , and the subsets of ` y ` of cardinality less
       than that of ` y ` . $)
    axgroth6 $p |- E. y ( x e. y /\ A. z e. y ( ~P z C_ y /\ ~P z e. y )
                   /\ A. z e. ~P y ( z ~< y -> z e. y ) ) $=
      ( vw vv cv wcel cpw wss wa wral csdm wbr wi w3a wex wrex cen wo axgroth5
      biid wb wceq pweq sseq1d cbvralv ssid sseq2 rcla4ev mpan2 rcla4cv wal
      pwss visset pwex sseq1 eleq1 imbi12d cla4v sylbi syl6 r19.23adv impbid2
      ralbidv pm5.32i r19.26 3bitr4i elpw wn cdom impexp cvv ssdomg ax-mp
      pm4.71i imbi1i brsdom bitri imbi2i 3bitr4ri pm5.74ri pm4.64 syl6bb
      ralbiia 3anbi123i exbii mpbir ) AFBFZGZCFZHZWHIZWKWHGZJCWHKZWJWHLMZWJWHGZ
      NZCWHHZKZOZBPWIWLWKDFZIZDWHQZJCWHKZWJWHRMZWPSZCWRKZOZBPABCDTWTXHBWIWIWNXD
      WSXGWIUAWLCWHKZWMCWHKZJXIXCCWHKZJWNXDXIXJXKXIEFZHZWHIZEWHKZXJXKUBWLXNCEWH
      WJXLUCWKXMWHWJXLUDUEUFXOWMXCCWHXOWMXCWMWKWKIZXCWKUGXBXPDWKWHXAWKWKUHUIUJX
      OXBWMDWHXOXAWHGXAHZWHIZXBWMNZXNXREXAWHXLXAUCXMXQWHXLXAUDUEUKXRXLXAIZXLWHG
      ZNZEULXSEXAWHUMYBXSEWKWJCUNZUOXLWKUCXTXBYAWMXLWKXAUPXLWKWHUQURUSUTVAVBVCV
      DUTVEWLWMCWHVFWLXCCWHVFVGWQXFCWRWJWRGWJWHIZWQXFUBWJWHYCVHYDWQXEVIZWPNZXFY
      DWQYFYDWJWHVJMZJZYFNYDYGYFNZNYDYFNYDWQNYDYGYFVKYDYHYFYDYGWJVLGYDYGNYCWJWH
      VLVMVNVOVPWQYIYDWQYGYEJZWPNYIWOYJWPWJWHVQVPYGYEWPVKVRVSVTWAXEWPWBWCUTWDWE
      WFWG $.
      $( [16-Jan-2011] $) $( [21-Jun-2009] $)

    $( The Tarksi-Grothendieck Axiom implies the Axiom of Infinity (in the form
       of ~ omex ).  Note that our proof depends on neither the Axiom of
       Infinity nor Regularity.  (Revised by Mario Carneiro, 19-Apr-2013.) $)
    grothomex $p |- om e. _V $=
      ( vy vz vx vw com cr1 cima cres wf1 cvv wcel wf1o con0 wss r111 omsson
      f1ores mp2an f1of1 ax-mp c0 cv cpw wral wa wex wel 0ex wceq eleq1 anbi1d
      exbidv csdm wbr wi w3a axgroth6 simpr ralimi anim2i 3adant3 eximi vtocl
      cfv wrex wfn wb r1fnon fvelimab csuc fveq2 eleq1d weq r10 eleq1i biimpri
      adantr pweq rcla4cv nnon r1suc syl biimprcd syl6 com3r adantld finds2
      biimpd syl9 r19.23aiv sylbi com12 ssrdv visset ssex 19.23aiv f1dmex ) EFE
      GZFEHZIZWRJKZEJKEWRWSLZWTMJFIEMNZXBOPMJEFQREWRWSSTUAAUBZKZBUBZUCZXDKZBXDU
      DZUEZAUFZXACAUGZXIUEZAUFZXKCUAUHCUBZUAUIZXMXJAXPXLXEXIXOUAXDUJUKULXLXGXDN
      ZXHUEZBXDUDZXFXDUMUNBAUGUOBXDUCUDZUPZAUFXNCABUQYAXMAXLXSXMXTXSXIXLXRXHBXD
      XQXHURUSUTVAVBTVCXJXAAXJWRXDNXAXJDWRXDDUBZWRKZXJDAUGZYCXOFVDZYBUIZCEVEZXJ
      YDUOZFMVFXCYCYGVGVHPCMEYBFVIRYFYHCEXOEKXJYEXDKZYFYDYIUAFVDZXDKZYBFVDZXDKZ
      YBVJZFVDZXDKZXJCDXPYEYJXDXOUAFVKVLCDVMYEYLXDXOYBFVKVLXOYNUIYEYOXDXOYNFVKV
      LXEYKXIYKXEYJUAXDVNVOVPVQYBEKZXIYMYPUOXEXIYMYQYPXIYMYLUCZXDKZYQYPUOXHYSBY
      LXDXFYLUIXGYRXDXFYLVRVLVSYQYPYSYQYOYRXDYQYBMKYOYRUIYBVTYBWAWBVLWCWDWEWFWG
      YFYIYDYEYBXDUJWHWIWJWKWLWMWRXDAWNWOWBWPTEWRJWSWQR $.
      $( [19-Apr-2013] $)

    $( The Tarksi-Grothendieck Axiom implies the Axiom of Infinity (in the form
       of ~ omex ).  Note that our proof does not depend on the Axiom of
       Infinity. $)
    grothomexOLD $p |- om e. _V $=
      ( vy vx vz vw vv cv wcel wss wi wal wrex wa wral cen wbr wo w3a wex
      ax-groth ssid wceq sseq1 elequ1 imbi12d a4v mpi reximi adantl ralimi
      rexbidv df-rex exancom bitri syl6bb cbvralv df-ral sylib anim2i 3adant3
      eximi ax-mp inf2 inf3 ) AABCBFZAFZGZCFZDFZHVGVEGZICJZEFZVHHZVKVGGZIZEJZCV
      EKZLZDVEMZVHVEHVHVENOVHVEGPIDJZQZARVFVFVDVGGZVILCRZIBJZLZARBADCESVTWDAVFV
      RWDVSVRWCVFVRVHVGGZCVEKZDVEMZWCVQWFDVEVPWFVJVOWECVEVOVHVHHZWEVHTVNWHWEIED
      VKVHUAVLWHVMWEVKVHVHUBEDCUCUDUEUFUGUHUIWGWBBVEMWCWFWBDBVEVHVDUAZWFWACVEKZ
      WBWIWEWACVEDBCUCUJWJVIWALCRWBWACVEUKVIWACULUMUNUOWBBVEUPUMUQURUSUTVAVBVC
      $.
      $( [23-Mar-2007] $)

    $( The Tarksi-Grothendieck Axiom implies the Axiom of Choice (in the form
       of ~ numth2 ).  This can be put in a more conventional form via ~ ween
       and ~ aceq8 .  Note that the mere existence of strongly inaccessible
       cardinals doesn't imply AC, but rather the particular form of the
       Tarski-Grothendieck axiom (see
       ~ http://www.cs.nyu.edu/pipermail/fom/2008-March/012783.html ).
       (Contributed by Mario Carneiro, 19-Apr-2013.) $)
    grothac $p |- E. x e. On x ~~ y $=
      ( vu vz cv cpw wss csdm wbr crab wa wex cdom con0 wrex cen wel wcel wral
      wi w3a axgroth6 weq pweq sseq1d eleq1d anbi12d rcla4va simpld rabss
      biimpri anim12i 3impa eximi ax-mp tskwe cvv grothpwex ssdomg visset
      canth2 sdomdom domtr mpan ensym domentr syl2an ex syl reximdv syl5 imp
      19.23aiv ondomen mp2b ) BEZFZCEZGZAEZVRHIZAVRFZJVRGZKZCLZVPDEZMIZDNOZVTVP
      PIANOBCQZVTFZVRGZWJVRRZKZAVRSZWAACQTAWBSZUAZCLWEBCAUBWPWDCWIWNWOWDWIWNKZV
      SWOWCWQVSVQVRRZWMVSWRKAVPVRABUCZWKVSWLWRWSWJVQVRVTVPUDZUEWSWJVQVRWTUFUGUH
      UIWCWOWAAWBVRUJUKULUMUNUOWDWHCVSWCWHWCWFVRPIZDNOVSWHADVRUPVSXAWGDNVSVQVRM
      IZXAWGTVQUQRVSXBTBURVQVRUQUSUOXBXAWGXBVPVRMIZVRWFPIWGXAVPVQMIZXBXCVPVQHIX
      DVPBUTVAVPVQVBUOVPVQVRVCVDWFVRCUTVEVPVRWFVFVGVHVIVJVKVLVMDAVPVNVO $.
      $( [19-Apr-2013] $)

    $( Alternate version of the Tarksi-Grothendieck Axiom. ~ ax-ac is used to
       derive this version. $)
    axgroth3 $p |- E. y ( x e. y /\ A. z e. y ( A. w ( w C_ z -> w e. y ) /\
                       E. w e. y A. v ( v C_ z -> v e. w ) ) /\
                     A. z ( z C_ y -> ( ( y \ z ) ~<_ z \/ z e. y ) ) ) $=
      ( cv wcel wss wi wal wrex wa wral cdif cdom wbr wo w3a wex axgroth2 cuni
      wb ssid wceq sseq1 elequ1 imbi12d a4v mpi reximi eluni2 sylibr adantl
      ralimi dfss3 com c0 wne ne0i visset dominf sylan infdif2 syl orbi1d
      imbi2d albidv sylan2 pm5.32i df-3an 3bitr4i exbii mpbir ) AFZBFZGZDFZCFZH
      VQVOGIDJZEFZVRHZVTVQGZIZEJZDVOKZLZCVOMZVRVOHZVOVRNVROPZVRVOGZQZIZCJZRZBSV
      PWGWHVOVROPZWJQZIZCJZRZBSABCDETWNWSBVPWGLZWMLWTWRLWNWSWTWMWRWGVPVOVOUAZHZ
      WMWRUBWGVRXAGZCVOMXBWFXCCVOWEXCVSWEVRVQGZDVOKXCWDXDDVOWDVRVRHZXDVRUCWCXEX
      DIECVTVRUDWAXEWBXDVTVRVRUEECDUFUGUHUIUJDVRVOUKULUMUNCVOXAUOULVPXBLZWLWQCX
      FWKWPWHXFWIWOWJXFUPVOOPZWIWOUBVPVOUQURXBXGVOVNUSVOBUTZVAVBVOVRXHCUTVCVDVE
      VFVGVHVIVPWGWMVJVPWGWRVJVKVLVM $.
      $( [26-Mar-2007] $)

    $( Alternate version of the Tarksi-Grothendieck Axiom. ~ ax-ac is used to
       derive this version. $)
    axgroth4 $p |- E. y ( x e. y /\ A. z e. y E. v e. y A. w ( w C_ z ->
    w e. ( y i^i v ) ) /\ A. z ( z C_ y -> ( ( y \ z ) ~<_ z \/ z e. y ) ) ) $=
      ( vu cv wcel wss wi wal wrex wa wral cdif cdom wbr wo w3a wex cin
      axgroth3 wceq elequ2 imbi2d albidv cbvrexv anbi2i r19.42v sseq1 elequ1
      imbi12d cbvalv 19.26 pm4.76 elin imbi2i bitr4i albii 3bitr2i rexbii
      ralbii 3anbi2i exbii mpbi ) AGBGZHZDGZCGZIZVHVFHZJZDKZFGZVIIZVNVHHZJZFKZD
      VFLZMZCVFNZVIVFIVFVIOVIPQVIVFHRJCKZSZBTVGVJVHVFEGZUAHZJZDKZEVFLZCVFNZWBSZ
      BTABCDFUBWCWJBWAWIVGWBVTWHCVFVTVMVOVNWDHZJZFKZEVFLZMVMWMMZEVFLWHVSWNVMVRW
      MDEVFVHWDUCZVQWLFWPVPWKVODEFUDUEUFUGUHVMWMEVFUIWOWGEVFWOVMVJVHWDHZJZDKZMV
      LWRMZDKWGWMWSVMWLWRFDVNVHUCVOVJWKWQVNVHVIUJFDEUKULUMUHVLWRDUNWTWFDWTVJVKW
      QMZJWFVJVKWQUOWEXAVJVHVFWDUPUQURUSUTVAUTVBVCVDVE $.
      $( [16-Apr-2007] $)
  $}

  ${
    $d x y z w v u t h g f n m k j $.
    $( Lemma for ~ grothprim .  Expand the membership of an unordered pair into
       primitives. $)
    grothprimlem $p |- ( { u , v } e. w <-> E. g ( g e. w /\ A. h
      ( h e. g <-> ( h = u \/ h = v ) ) ) ) $=
      ( cv cpr wcel wceq wo cab wb wal wa wex dfpr2 eleq1i clabel bitri ) CFZBF
      ZGZAFZHEFZTIUDUAIJZEKZUCHDFZUCHUDUGHUELEMNDOUBUFUCETUAPQUEEDUCRS $.
      $( [29-Mar-2007] $)

    $( The Tarksi-Grothendieck Axiom ~ ax-groth expanded into set theory
       primitives using 163 symbols.  An open problem is whether a shorter
       equivalent exists (when expanded to primitives). $)
    grothprim $p |- E. y ( x e. y /\ A. z ( ( z e. y -> E. v ( v e. y /\
       A. w ( A. u ( u e. w -> u e. z ) -> ( w e. y /\ w e. v ) ) ) ) /\ E. w
      ( ( w e. z -> w e. y ) -> ( A. v ( ( v e. z -> E. t A. u ( E. g ( g e. w
      /\ A. h ( h e. g <-> ( h = v \/ h = u ) ) ) -> u = t ) ) /\ ( v e. y ->
      ( v e. z \/ E. u ( u e. z /\ E. g ( g e. w /\ A. h ( h e. g <-> ( h = u
      \/ h = v ) ) ) ) ) ) ) \/ z e. y ) ) ) ) $=
      ( cv wcel wss cin wi wal wrex wral cdif cdom wbr wo w3a wex wa wceq wb
      axgroth4 3anass dfss2 elin imbi12i albii rexbii df-rex bitri ralbii
      df-ral cpr wmo cvv visset difexg ax-mp c0 incom difdisj eqtri brdom6disj
      orbi1i ax-17 19.44 bitr4i 19.35 grothprimlem mobii mo2 wn impexp eldif
      df-or imbi2i 3bitr4i anbi12i 19.26 exbii 3bitr2i anbi2i mpbi ) AJBJZKZDJZ
      CJZLZWKWIEJZMKZNZDOZEWIPZCWIQZWLWILZWIWLRZWLSTZWLWIKZUAZNZCOZUBZBUCWJXCWN
      WIKZFJZWKKXIWLKZNFOZWKWIKZWKWNKUDZNZDOZUDEUCZNZWKWLKXLNZWNWLKZHJZWKKZIJZX
      TKZYBWNUEZYBXIUEZUAUFIOUDHUCZXIGJUENFOGUCZNZXHXSXJYAYCYEYDUAUFIOUDHUCZUDF
      UCZUAZNZUDEOZXCUAZNZDUCZUDCOZUDZBUCABCDEUGXGYRBXGWJWSXFUDZUDYRWJWSXFUHYSY
      QWJYSXQCOZYPCOZUDYQWSYTXFUUAWSXPCWIQYTWRXPCWIWRXOEWIPXPWQXOEWIWPXNDWMXKWO
      XMFWKWLUIWKWIWNUJUKULUMXOEWIUNUOUPXPCWIUQUOXEYPCXEXRDOZWNXIURWKKZFUSZEWLQ
      ZXIWNURWKKZFWLPZEXAQZUDZXCUAZDUCZNXRUUJNZDUCYPWTUUBXDUUKDWLWIUIXDUUIDUCZX
      CUAUUKXBUUMXCEFXAWLDWIUTKXAUTKBVAWIWLUTVBVCCVAXAWLMWLXAMVDXAWLVEWLWIVFVGV
      HVIUUIXCDXCDVJVKVLUKXRUUJDVMUULYODUUJYNXRUUIYMXCUUIYHEOZYLEOZUDYMUUEUUNUU
      HUUOUUEYGEWLQUUNUUDYGEWLUUDYFFUSYGUUCYFFDFEHIVNVOYFFGYFGVJVPUOUPYGEWLUQUO
      UUHWNXAKZUUGNZEOUUOUUGEXAUQUUQYLEXHXSVQZUDZYJNXHUURYJNZNUUQYLXHUURYJVRUUP
      UUSUUGYJWNWIWLVSUUGYIFWLPYJUUFYIFWLDEFHIVNUMYIFWLUNUOUKYKUUTXHXSYJVTWAWBU
      LUOWCYHYLEWDVLVIWAWEWFULWCXQYPCWDVLWGUOWEWH $.
      $( [16-Apr-2007] $)
  $}

  ${
    $d T w x y z $.
    $( The Tarski-Grothendieck Axiom, using abbreviations.  (Contributed by
       Mario Carneiro, 28-May-2013.) $)
    grothtsk $p |- U. Tarski = _V $=
      ( vw vx vy vz ctsk cuni cvv cv wcel wa wex cpw wss wrex wral cen wbr wo
      w3a axgroth5 visset eltsk anbi2i 3anass bitr4i exbii mpbir eluni 2th
      eqriv ) AEFZGAHZUKIZULGIUMULBHZIZUNEIZJZBKZURUOCHZLZUNMUTDHMDUNNJCUNOZUSU
      NPQUSUNIRCUNLOZSZBKABCDTUQVCBUQUOVAVBJZJVCUPVDUOCDUNBUAUBUCUOVAVBUDUEUFUG
      BULEUHUGAUAUIUJ $.
      $( [28-May-2013] $)
  $}

  ${
    $d w x y z $.
    $( An equivalent to the Tarski-Grothendieck Axiom: there is a proper class
       of inaccessible cardinals.  (Contributed by Mario Carneiro,
       9-Jun-2013.) $)
    inaprc $p |- Inacc e/ _V $=
      ( vx vy vz vw cina cvv wnel cuni con0 wceq wss word cv wcel cwina
      inawina winaon syl ssriv ssorduni ordsson mp2b wrex ctsk visset grothtsk
      eleqtrri eluni2 mpbi wa ccrd cfv c0 wne ne0i tskcard sylan2 adantl csdm
      wbr tsksdom wb cardsdomel adantr mpbid eleq2 rcla4ev syl11anc exp32
      r19.23adv mpi sylibr eqssi ssonprc ax-mp mpbir ) EFGZEHZIJZVRIEIKZVRLVRIK
      AEIAMZENWAONWAINWAPWAQRSZETVRUAUBBIVRBMZINZWCCMZNZCEUCZWCVRNWDWCDMZNZDUDU
      CZWGWCUDHZNWJWCFWKBUEUFUGDWCUDUHUIWDWIWGDUDWDWHUDNZWIWGWDWLWIUJZUJZWHUKUL
      ZENZWCWONZWGWMWPWDWIWLWHUMUNWPWHWCUOWHUPUQURWNWCWHUSUTZWQWMWRWDWCWHVAURWD
      WRWQVBWMWCWHVCVDVEWFWQCWOEWEWOWCVFVGVHVIVJVKCWCEUHVLSVMVTVQVSVBWBEVNVOVP
      $.
      $( [9-Jun-2013] $)
  $}

