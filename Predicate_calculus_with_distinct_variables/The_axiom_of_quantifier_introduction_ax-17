$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            The axiom of quantifier introduction ax-17
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x ps $.
    a4imv.1 $e |- ( x = y -> ( ph -> ps ) ) $.
    $( A version of ~ a4im with a distinct variable requirement instead of a
       bound variable hypothesis. $)
    a4imv $p |- ( A. x ph -> ps ) $=
      ( ax-17 a4im ) ABCDBCFEG $.
      $( [5-Aug-1993] $)
  $}

  ${
    $v f $.
    $( Define a temporary individual variable. $)
    aev.vf $f set f $.
    $d f u v $.  $d f u x y $.  $d u w $.
    $( A "distinctor elimination" lemma with no restrictions on variables in
       the consequent, proved without using ~ ax-16 .  (The proof was shortened
       by Andrew Salmon, 21-Jun-2011.) $)
    aev $p |- ( A. x x = y -> A. z w = v ) $=
      ( aev.vf vu weq wal hbae ax-8 a4imv 19.21ai equcomi syl6 alequcoms a5i
      alequcom 3syl ) ABHZAIZDEHZCABCJUAFBHZFIZGEHZGIZUBUAUCFABFJTUCAFAFBKLMUDF
      GHZFIZEGHZEIUFUCUGFUGBFBFHZUGBGBGHUJGFHUGBGFKGFNOLPQUHUIEFGEJUGUIFEFEGKLM
      EGRSUEUBGDGDEKLSM $.
      $( [26-Jun-2011] $) $( [8-Nov-2006] $)

    $( A "distinctor elimination" lemma with no restrictions on variables in
       the consequent, proved without using ~ ax-16 .  The proof is unusual in
       that it involves linking 17 implications, which might provide an
       interesting challenge for an automated theorem prover. $)
    aevOLD $p |- ( A. x x = y -> A. z w = v ) $=
      ( vu aev.vf weq wal hbae alequcom ax-8 a4imv equcomi 3syl alimi a5i
      19.21bi 19.21ai ) ABHAIZDEHZCABCJTUADTTAIAFHZAIZUADIZABAJTUBATBAHZBIFAHZU
      BABKUEUFBFBFALMFANOPUCUFFIZFGHZFIZUDAFKUFUHFUGUCGFHZUHFAKUBUJAGAGFLMGFNOQ
      UIUIFIFEHZFIZUDFGFJUIUKFUIUJGIEFHZUKFGKUJUMGEGEFLMEFNOPULUMEIZEDHZEIUDFEK
      UMUOEUNULUAUOEFKUKUAFDFDELMDENOQEDKOOOORS $.
      $( [8-Nov-2006] $)
  $}

