$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            The axiom of quantifier introduction.ax-17
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x ph $.
    $( Axiom to quantify a variable over a formula in which it does not occur.
       Axiom C5 in [Megill] p. 444 (p. 11 of the preprint).  Also appears as
       Axiom B6 (p. 75) of system S2 of [Tarski] p. 77.  If we allow the
       otherwise redundant ~ ax-15 and ~ ax-16 , this axiom is logically
       redundant since it is a metatheorem justified by induction on the number
       of primitive connectives in wff ` ph ` , using ~ ax17eq and ~ ax17el
       together ~ hbn , ~ hbal , and ~ hbim .  However, if we omit this axiom
       our development would be quite inconvenient since we could work only
       with specific instances of wffs containing no wff variables - this axiom
       is effectively a definition of the concept of a set variable not
       occurring in a wff (as opposed to just two set variables being
       distinct). $)
    ax-17 $a |- ( ph -> A. x ph ) $.
  $}

  ${
    $d x ps $.
    a4b.1 $e |- ( x = y -> ( ph -> ps ) ) $.
    $( A weaker version of ~ a4a . $)
    a4b $p |- ( A. x ph -> ps ) $=
      ( ax-17 a4a ) ABCDBCFEG $.
      $( [5-Aug-1993] $)
  $}

  ${
    $v f $.
    $( Define a temporary individual variable. $)
    aev.vf $f set f $.
    $d f u v $.  $d f u x y $.  $d u w $.
    $( A "distinctor elimination" lemma with no restrictions on variables in
       the consequent, proved without using ~ ax-16 .  The proof is unusual in
       that it involves linking 17 implications, which might provide an
       interesting challenge for an automated theorem prover. $)
    aev $p |- ( A. x x = y -> A. z w = v ) $=
      ( vu aev.vf weq wal hbae alequcom ax-8 a4b equcomi 3syl 19.20i a5i
      19.21bi 19.21ai ) ABHAIZDEHZCABCJTUADTTAIAFHZAIZUADIZABAJTUBATBAHZBIFAHZU
      BABKUEUFBFBFALMFANOPUCUFFIZFGHZFIZUDAFKUFUHFUGUCGFHZUHFAKUBUJAGAGFLMGFNOQ
      UIUIFIFEHZFIZUDFGFJUIUKFUIUJGIEFHZUKFGKUJUMGEGEFLMEFNOPULUMEIZEDHZEIUDFEK
      UMUOEUNULUAUOEFKUKUAFDFDELMDENOQEDKOOOORS $.
      $( [8-Nov-2006] $) $( [8-Nov-2006] $)
  $}

