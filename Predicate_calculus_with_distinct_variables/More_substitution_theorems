$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More substitution theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$( The theorems in this section make use of the $d statement. $)

  ${
    $d y ph $.
    $( Two ways of expressing " ` x ` is not free in ` ph ` ." $)
    sbhb $p |- ( ( ph -> A. x ph ) <-> A. y ( ph -> [ y / x ] ph ) ) $=
      ( wal wi wsb ax-17 sb8 imbi2i 19.21v bitr4i ) AABDZEAABCFZCDZEAMECDLNAABC
      ACGHIAMCJK $.
      $( [29-May-2009] $)
  $}

  ${
    $d y z $.  $d x y $.
    $( Lemma for ~ equsb3 .  (The proof was shortened by Andrew Salmon,
       14-Jun-2011.) $)
    equsb3lem $p |- ( [ x / y ] y = z <-> x = z ) $=
      ( cv wceq ax-17 equequ1 sbie ) BDCDZEADIEZBAJBFBACGH $.
      $( [14-Jun-2011] $) $( [4-Dec-2005] $)
  $}

  ${
    $d y z $.  $d x y $.
    $( Lemma for ~ equsb3 . $)
    equsb3lemOLD $p |- ( [ x / y ] y = z <-> x = z ) $=
      ( weq wsb wb equsb2 equequ1 sbimi ax-mp sbbi mpbi ax-17 sbf bitr3i ) BCDZ
      BAEZACDZBAEZRRPFZBAEZSQFABDZBAEUABAGUBTBAABCHIJRPBAKLRBARBMNO $.
      $( [4-Dec-2005] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic wff.  (Contributed by Raph Levien and
       FL, 4-Dec-2005.) $)
    equsb3 $p |- ( [ x / y ] y = z <-> x = z ) $=
      ( vw weq wsb equsb3lem sbbii ax-17 sbco2 3bitr3i ) BCEZBDFZDAFDCEZDAFLBAF
      ACEMNDADBCGHLBADLDIJADCGK $.
      $( [4-Dec-2005] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic membership wff.  (The proof was
       shortened by Andrew Salmon, 14-Jun-2011.) $)
    elsb3 $p |- ( [ x / y ] y e. z <-> x e. z ) $=
      ( vw wel wsb ax-17 elequ1 sbie sbbii sbco2 bitr3i wb weq equsb1 sbimi
      ax-mp sbbi mpbi sbf 3bitri ) BCEZBAFZDCEZDAFZACEZDAFZUFUCUDDBFZBAFUEUHUBB
      AUDUBDBUBDGDBCHIJUDDABUDBGKLUDUFMZDAFZUEUGMDANZDAFUJDAOUKUIDADACHPQUDUFDA
      RSUFDAUFDGTUA $.
      $( [14-Jun-2011] $) $( [7-Nov-2006] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic membership wff. $)
    elsb3OLD $p |- ( [ x / y ] y e. z <-> x e. z ) $=
      ( vw wel wsb wb weq equsb2 elequ1 sbimi ax-mp sbbi mpbi ax-17 sbf bitr3i
      sbbii sbco2 3bitr3i ) BCEZBDFZDAFDCEZDAFZUABAFACEZUBUCDAUBUCBDFZUCUCUAGZB
      DFZUFUBGDBHZBDFUHBDIUIUGBDDBCJKLUCUABDMNUCBDUCBOPQRUABADUADOSUDUEDAFZUEUE
      UCGZDAFZUJUDGADHZDAFULDAIUMUKDAADCJKLUEUCDAMNUEDAUEDOPQT $.
      $( [7-Nov-2006] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic membership wff.  (Contributed by
       Rodolfo Medina, 3-Apr-2010.)  (The proof was shortened by Andrew Salmon,
       14-Jun-2011.) $)
    elsb4 $p |- ( [ x / y ] z e. y <-> z e. x ) $=
      ( vw wel wsb ax-17 elequ2 sbie sbbii sbco2 bitr3i wb weq equsb1 sbimi
      ax-mp sbbi mpbi sbf 3bitri ) CBEZBAFZCDEZDAFZCAEZDAFZUFUCUDDBFZBAFUEUHUBB
      AUDUBDBUBDGDBCHIJUDDABUDBGKLUDUFMZDAFZUEUGMDANZDAFUJDAOUKUIDADACHPQUDUFDA
      RSUFDAUFDGTUA $.
      $( [14-Jun-2011] $) $( [3-Apr-2010] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic membership wff.  (Contributed by
       Rodolfo Medina, 3-Apr-2010.) $)
    elsb4OLD $p |- ( [ x / y ] z e. y <-> z e. x ) $=
      ( vw wel wsb wb weq equsb2 elequ2 sbimi ax-mp sbbi mpbi ax-17 sbf bitr3i
      sbbii sbco2 3bitr3i ) CBEZBDFZDAFCDEZDAFZUABAFCAEZUBUCDAUBUCBDFZUCUCUAGZB
      DFZUFUBGDBHZBDFUHBDIUIUGBDDBCJKLUCUABDMNUCBDUCBOPQRUABADUADOSUDUEDAFZUEUE
      UCGZDAFZUJUDGADHZDAFULDAIUMUKDAADCJKLUEUCDAMNUEDAUEDOPQT $.
      $( [3-Apr-2010] $)
  $}

  ${
    $d x y $.
    $( ` x ` is not free in ` [ y / x ] ph ` when ` x ` and ` y ` are
       distinct. $)
    hbs1 $p |- ( [ y / x ] ph -> A. x [ y / x ] ph ) $=
      ( weq wal wsb wi ax-16 hbsb2 pm2.61i ) BCDBEABCFZKBEGKBCHABCIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d y z $.
    hbsb.1 $e |- ( ph -> A. z ph ) $.
    $( If ` z ` is not free in ` ph ` , it is not free in ` [ y / x ] ph ` when
       ` y ` and ` z ` are distinct. $)
    hbsb $p |- ( [ y / x ] ph -> A. z [ y / x ] ph ) $=
      ( weq wal wsb wi ax-16 hbsb4 pm2.61i ) DCFDGABCHZMDGIMDCJABCDEKL $.
      $( [12-Aug-1993] $)
  $}

  ${
    $d x z $.  $d x w $.  $d y z $.
    $( Commutativity law for substitution.  Used in proof of Theorem 9.7 of
       [Megill] p. 449 (p. 16 of the preprint). $)
    sbcom2 $p |- ( [ w / z ] [ y / x ] ph <-> [ y / x ] [ w / z ] ph ) $=
      ( weq wal wsb wb wn wa wi alcom bi2.04 albii 19.21v bitri 3bitr3i a1i
      sb4b imbi2d albidv sylan9bbr sylan9bb 3bitr4d ex hbae sbequ12 a4s sbbid
      bitr3d pm2.61ii ) BCFZBGZDEFZDGZABCHZDEHZADEHZBCHZIZUNJZUPJZVAVBVCKZUOUMA
      LZBGZLZDGZUMUOALZDGZLZBGZURUTVHVLIVDUMVILZBGZDGVMDGZBGVHVLVMDBMVNVGDVNUOV
      ELZBGVGVMVPBUMUOANOUOVEBPQOVOVKBUMVIDPORSVCURUOUQLZDGVBVHUQDETVBVQVGDVBUQ
      VFUOABCTUAUBUCVBUTUMUSLZBGVCVLUSBCTVCVRVKBVCUSVJUMADETUAUBUDUEUFUNUSURUTU
      NAUQDEBCDUGUMAUQIBABCUHUIUJUMUSUTIBUSBCUHUIUKUPUQURUTUOUQURIDUQDEUHUIUPAU
      SBCDEBUGUOAUSIDADEUHUIUJUKUL $.
      $( [27-May-1997] $)
  $}

  ${
    $d x y z $.  $d w y $.
    $( Equivalence for double substitution. $)
    2sb5 $p |- ( [ z / x ] [ w / y ] ph <->
               E. x E. y ( ( x = z /\ y = w ) /\ ph ) ) $=
      ( wsb weq wa wex sb5 19.42v anass exbii anbi2i 3bitr4ri bitri ) ACEFZBDFB
      DGZQHZBIRCEGZHAHZCIZBIQBDJSUBBRTAHZHZCIRUCCIZHUBSRUCCKUAUDCRTALMQUERACEJN
      OMP $.
      $( [3-Feb-2005] $)

    $( Equivalence for double substitution. $)
    2sb6 $p |- ( [ z / x ] [ w / y ] ph <->
               A. x A. y ( ( x = z /\ y = w ) -> ph ) ) $=
      ( wsb weq wi wal wa sb6 19.21v impexp albii imbi2i 3bitr4ri bitri ) ACEFZ
      BDFBDGZRHZBISCEGZJAHZCIZBIRBDKTUCBSUAAHZHZCISUDCIZHUCTSUDCLUBUECSUAAMNRUF
      SACEKOPNQ $.
      $( [3-Feb-2005] $)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution. $)
    sb6a $p |- ( [ y / x ] ph <-> A. x ( x = y -> [ x / y ] ph ) ) $=
      ( wsb weq wi wal sb6 wb sbequ12 equcoms pm5.74i albii bitri ) ABCDBCEZAFZ
      BGOACBDZFZBGABCHPRBOAQAQICBACBJKLMN $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x w $.  $d y z $.  $d z w $.
    2sb5rf.1 $e |- ( ph -> A. z ph ) $.
    2sb5rf.2 $e |- ( ph -> A. w ph ) $.
    $( Reversed double substitution. $)
    2sb5rf $p |- ( ph <->
                E. z E. w ( ( z = x /\ w = y ) /\ [ z / x ] [ w / y ] ph ) ) $=
      ( weq wsb wa wex sb5rf 19.42v sbcom2 anbi2i anass bitri exbii hbsb
      3bitr4ri ) ADBHZABDIZJZDKUAECHZJZACEIBDIZJZEKZDKABDFLUCUHDUAUDUBCEIZJZJZE
      KUAUJEKZJUHUCUAUJEMUGUKEUGUEUIJUKUFUIUEACEBDNOUAUDUIPQRUBULUAUBCEABDEGSLO
      TRQ $.
      $( [3-Feb-2005] $)

    $( Reversed double substitution. $)
    2sb6rf $p |- ( ph <->
                A. z A. w ( ( z = x /\ w = y ) -> [ z / x ] [ w / y ] ph ) ) $=
      ( weq wsb wi wal wa sb6rf 19.21v sbcom2 imbi2i impexp bitri albii hbsb
      3bitr4ri ) ADBHZABDIZJZDKUBECHZLZACEIBDIZJZEKZDKABDFMUDUIDUBUEUCCEIZJZJZE
      KUBUKEKZJUIUDUBUKENUHULEUHUFUJJULUGUJUFACEBDOPUBUEUJQRSUCUMUBUCCEABDEGTMP
      UASR $.
      $( [3-Feb-2005] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z ph $.
    $( An alternate definition of proper substitution ~ df-sb .  By introducing
       a dummy variable ` z ` in the definiens, we are able to eliminate any
       distinct variable restrictions among the variables ` x ` , ` y ` , and
       ` ph ` of the definiendum.  No distinct variable conflicts arise because
       ` z ` effectively insulates ` x ` from ` y ` .  To achieve this, we use
       a chain of two substitutions in the form of ~ sb5 , first ` z ` for
       ` x ` then ` y ` for ` z ` .  Compare Definition 2.1'' of [Quine] p. 17,
       which is obtained from this theorem by applying ~ df-clab .  Theorem
       ~ sb7f provides a version where ` ph ` and ` z ` don't have to be
       distinct. $)
    dfsb7 $p |- ( [ y / x ] ph <-> E. z ( z = y /\ E. x ( x = z /\ ph ) ) ) $=
      ( wsb weq wa wex sb5 sbbii ax-17 sbco2 3bitr3i ) ABDEZDCEBDFAGBHZDCEABCED
      CFOGDHNODCABDIJABCDADKLODCIM $.
      $( [28-Jan-2004] $)
  $}

  ${
    $d w x z $.  $d w y z $.  $d w ph $.
    sb7f.1 $e |- ( ph -> A. z ph ) $.
    $( This version of ~ dfsb7 does not require that ` ph ` and ` z ` be
       distinct.  This permits it to be used as a definition for substitution
       in a formalization that omits the logically redundant axiom ~ ax-17 i.e.
       that doesn't have the concept of a variable not occurring in a wff.
       ( ~ df-sb is also suitable, but its mixing of free and bound variables
       is distasteful to some logicians.)  (The proof was shortened by Andrew
       Salmon, 25-May-2011.) $)
    sb7f $p |- ( [ y / x ] ph <->
               E. z ( z = y /\ E. x ( x = z /\ ph ) ) ) $=
      ( wsb weq wa wex sb5 sbbii sbco2 3bitr3i ) ABDFZDCFBDGAHBIZDCFABCFDCGOHDI
      NODCABDJKABCDELODCJM $.
      $( [25-May-2011] $) $( [26-Jul-2006] $)

    $( This version of ~ dfsb7 does not require that ` ph ` and ` z ` be
       distinct.  This permits it to be used as a definition for substitution
       in a formalization that omits the logically redundant axiom ~ ax-17 i.e.
       that doesn't have the concept of a variable not occurring in a wff.
       ( ~ df-sb is also suitable, but its mixing of free and bound variables
       is distasteful to some logicians.) $)
    sb7fOLD $p |- ( [ y / x ] ph <->
               E. z ( z = y /\ E. x ( x = z /\ ph ) ) ) $=
      ( vw wsb weq wa wex dfsb7 ax-17 hban hbex equequ1 equequ2 anbi1d exbidv
      anbi12d cbvex bitri ) ABCGFCHZBFHZAIZBJZIZFJDCHZBDHZAIZBJZIZDJABCFKUFUKFD
      UBUEDUBDLUDDBUCADUCDLEMNMUKFLFDHZUBUGUEUJFDCOULUDUIBULUCUHAFDBPQRSTUA $.
      $( [26-Jul-2006] $)
  $}

  ${
    $d x y $.
    sb10f.1 $e |- ( ph -> A. x ph ) $.
    $( Hao Wang's identity axiom P6 in Irving Copi, _Symbolic Logic_ (5th ed.,
       1979), p. 328.  In traditional predicate calculus, this is a sole axiom
       for identity from which the usual ones can be derived. $)
    sb10f $p |- ( [ y / z ] ph <-> E. x ( x = y /\ [ x / z ] ph ) ) $=
      ( weq wsb wa wex hbsb sbequ equsex bicomi ) BCFADBGZHBIADCGZNOBCADCBEJABC
      DKLM $.
      $( [9-May-2005] $)
  $}

  ${
    $d x ph $.
    $( An identity law for substitution.  Used in proof of Theorem 9.7 of
       [Megill] p. 449 (p. 16 of the preprint). $)
    sbid2v $p |- ( [ y / x ] [ x / y ] ph <-> ph ) $=
      ( ax-17 sbid2 ) ABCABDE $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x ph $.
    $( Elimination of substitution. $)
    sbelx $p |- ( ph <-> E. x ( x = y /\ [ x / y ] ph ) ) $=
      ( wsb weq wa wex sbid2v sb5 bitr3i ) AACBDZBCDBCEKFBGABCHKBCIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $( Note:  A more general case could also be proved with
       "$d x z $.  $d y w $.  $d x ph $.  $d y ph $.", but with more
       difficulty. $)
    $d x y z $.  $d w y $.  $d x y ph $.
    $( Elimination of double substitution. $)
    sbel2x $p |- ( ph <-> E. x E. y ( ( x = z /\ y = w ) /\
                     [ y / w ] [ x / z ] ph ) ) $=
      ( weq wsb wa wex sbelx anbi2i exbii exdistr 3bitr4i anass 2exbii bitr4i
      ) ABDFZCEFZADBGZECGZHZHZCIBIZRSHUAHZCIBIRTHZBIRUBCIZHZBIAUDUFUHBTUGRTCEJK
      LABDJRUBBCMNUEUCBCRSUAOPQ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.
    $( A theorem used in elimination of disjoint variable restriction on ` x `
       and ` y ` by replacing it with a distinctor ` -. A. x x = z ` . $)
    sbal1 $p |- ( -. A. x x = z ->
             ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) ) $=
      ( weq wal wn wsb wb wi sbequ12 a4s dral2 bitr3d a1d wa hba1 hbsb4 ax-4
      sbimi alimi syl6 adantl sb4 al2imi hbnaes ax-7 ax-16 ax-12 pm2.61i alim
      syl9 sb2 sylan9 impbid ex ) CDEZCFZBDEBFGZABFZCDHZACDHZBFZIZJURVDUSURUTVA
      VCUQUTVAICUTCDKLAVBCDBUQAVBICACDKLMNOURGZUSVDVEUSPVAVCUSVAVCJVEUSVAVABFVC
      UTCDBABQRVAVBBUTACDABSTUAUBUCVEVCUQAJZBFZCFZUSVAVEVCVFCFZBFZVHVCVJJCDBVEV
      BVIBACDUDUEUFVFBCUGUBVHVAJBDCUSCFVHUQUTJZCFVAUSVGVKCUSUQUQBFZVGUTBCEBFZUS
      UQVLJZJVMVNUSUQBCUHOCDBUIUJUQABUKULUEUTCDUMUBUFUNUOUPUJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x z $.
    $( Move universal quantifier in and out of substitution. $)
    sbal $p |- ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) $=
      ( weq wal wsb wb a16gb sbimi sbequ5 sbbi 3imtr3i bitr3d sbal1 pm2.61i )
      BDEBFZABFZCDGZACDGZBFZHQTSUAQCDGARHZCDGQTSHQUBCDABDBIJBDCDKARCDLMTBDBINAB
      CDOP $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x z $.
    $( Move existential quantifier in and out of substitution. $)
    sbex $p |- ( [ z / y ] E. x ph <-> E. x [ z / y ] ph ) $=
      ( wn wal wsb wex sbn sbal albii bitri notbii df-ex sbbii 3bitr4i ) AEZBFZ
      EZCDGZACDGZEZBFZEZABHZCDGUABHTRCDGZEUDRCDIUFUCUFQCDGZBFUCQBCDJUGUBBACDIKL
      MLUESCDABNOUABNP $.
      $( [27-Sep-2003] $)
  $}

  ${
    $d x z $.  $d y z $.
    sbalv.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Quantify with new variable inside substitution. $)
    sbalv $p |- ( [ y / x ] A. z ph <-> A. z ps ) $=
      ( wal wsb sbal albii bitri ) AEGCDHACDHZEGBEGAECDILBEFJK $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( An equivalent expression for existence. $)
    exsb $p |- ( E. x ph <-> E. y A. x ( x = y -> ph ) ) $=
      ( wex wsb weq wi wal ax-17 sb8e sb6 exbii bitri ) ABDABCEZCDBCFAGBHZCDABC
      ACIJNOCABCKLM $.
      $( [2-Feb-2005] $)
  $}

  ${
    $d x y z $.  $d y w $.  $d z w ph $.
    $( An equivalent expression for double existence. $)
    2exsb $p |- ( E. x E. y ph <->
                  E. z E. w A. x A. y ( ( x = z /\ y = w ) -> ph ) ) $=
      ( wex weq wi wal wa exsb exbii excom bitri impexp albii 19.21v bitr2i
      3bitri ) ACFZBFZCEGZAHZCIZBFZEFZBDGZUBJAHZCIZBIZDFZEFUJEFDFUAUDEFZBFUFTUL
      BACEKLUDBEMNUEUKEUEUGUDHZBIZDFUKUDBDKUNUJDUMUIBUIUGUCHZCIUMUHUOCUGUBAOPUG
      UCCQRPLNLUJEDMS $.
      $( [2-Feb-2005] $)
  $}

  ${
    $d z ps $.
    dvelim.1 $e |- ( ph -> A. x ph ) $.
    dvelim.2 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( This theorem can be used to eliminate a distinct variable restriction on
       ` x ` and ` z ` and replace it with the "distinctor" ` -. A. x x = y `
       as an antecedent. ` ph ` normally has ` z ` free and can be read
       ` ph ( z ) ` , and ` ps ` substitutes ` y ` for ` z ` and can be read
       ` ph ( y ) ` .  We don't require that ` x ` and ` y ` be distinct: if
       they aren't, the distinctor will become false (in multiple-element
       domains of discourse) and "protect" the consequent.

       To obtain a closed-theorem form of this inference, prefix the hypotheses
       with ` A. x A. z ` , conjoin them, and apply ~ dvelimdf .

       Other variants of this theorem are ~ dvelimf (with no distinct variable
       restrictions), ~ dvelimfALT (that avoids ~ ax-11 ), and ~ dvelimALT
       (that avoids ~ ax-10 ). $)
    dvelim $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( ax-17 dvelimf ) ABCDEFBEHGI $.
      $( [23-Nov-1994] $)
  $}

  ${
    $d z ps $.  $d x z $.  $d y z $.
    dvelimALT.1 $e |- ( ph -> A. x ph ) $.
    dvelimALT.2 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( Version of ~ dvelim that doesn't use ~ ax-10 .  (See ~ dvelimfALT for a
       version that doesn't use ~ ax-11 .) $)
    dvelimALT $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wi ax-17 hba1 ax16ALT a1i hbimd a1d wa hbn hban ax-12 imp
      ex pm2.61i hbald equsal albii 3imtr3g ) CDHZCIZJZEDHZAKZEIZUNCIBBCIUKUMCE
      UKELCEHZCIZUKUMUMCIKZKUPUQUKUPULACUOCMZULCENAACIKZUPFOPQUPJZUKUQUTUKRZULA
      CUTUKCUPCURSUJCUICMSTUTUKULULCIKEDCUAUBUSVAFOPUCUDUEABEDBELGUFZUNBCVBUGUH
      $.
      $( [17-May-2008] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveeq1 $p |- ( -. A. x x = y -> ( y = z -> A. x y = z ) ) $=
      ( vw weq ax-17 equequ1 dvelimfALT ) DCEZBCEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)

    $( Version of ~ dveeq1 using ~ ax-16 instead of ~ ax-17 . $)
    dveeq1ALT $p |- ( -. A. x x = y -> ( y = z -> A. x y = z ) ) $=
      ( vw weq ax17eq equequ1 dvelimfALT ) DCEBCEABDDCAFBCDFDBCGH $.
      $( [29-Apr-2008] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveel1 $p |- ( -. A. x x = y -> ( y e. z -> A. x y e. z ) ) $=
      ( vw wel ax-17 elequ1 dvelimfALT ) DCEZBCEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveel2 $p |- ( -. A. x x = y -> ( z e. y -> A. x z e. y ) ) $=
      ( vw wel ax-17 elequ2 dvelimfALT ) CDEZCBEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d z y $.  $d z x $.
    $( Move quantifier in and out of substitution. $)
    sbal2 $p |- ( -. A. x x = y ->
             ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) ) $=
      ( weq wal wn wi wsb hbnae wb dveeq1 alimi hbnaes 19.21t syl albid alcom
      syl5rbbr sb6 albii 3bitr4g ) BCEBFGZCDEZABFZHZCFZUDAHZCFZBFZUECDIACDIZBFU
      CUHBFZCFUGUJUCULUFCBCCJUCUDUDBFHZBFZULUFKUNBCBUCUMBBCDLMNUDABOPQUHCBRSUEC
      DTUKUIBACDTUAUB $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d w y $.  $d w z $.  $d w x $.  $( ` w ` is dummy. $)
    $( Axiom ~ ax-15 is redundant if we assume ~ ax-17 .  Remark 9.6 in
       [Megill] p. 448 (p. 16 of the preprint), regarding axiom scheme C14'.

       Note that ` w ` is a dummy variable introduced in the proof.  On the web
       page, it is implicitly assumed to be distinct from all other variables.
       (This is made explicit in the database file set.mm).  Its purpose is to
       satisfy the distinct variable requirements of ~ dveel2 and ~ ax-17 .  By
       the end of the proof it has vanished, and the final theorem has no
       distinct variable requirements.

       This theorem should not be referenced in any proof.  Instead, use
       ~ ax-15 below so that theorems needing ~ ax-15 can be more easily
       identified. $)
    ax15 $p |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x e. y -> A. z x e. y ) ) ) $=
      ( vw weq wal wn wel wi hbn1 dveel2 hbim1 ax-17 elequ1 imbi2d dvelimfALT
      19.21 syl6ib pm2.86d ) CAECFGZCBEZCFGZABHZUCCFZTUBUCIZUECFUBUDIUBDBHZIUEC
      ADUBUFCUACJZCBDKLUEDMDAEUFUCUBDABNOPUBUCCUGQRS $.
      $( [29-Jun-1995] $)
  $}

  $( Axiom of Quantifier Introduction.  One of the equality and substitution
     axioms for a non-logical predicate in our predicate calculus with
     equality.  Axiom scheme C14' in [Megill] p. 448 (p. 16 of the preprint).
     It is redundant if we include ~ ax-17 ; see theorem ~ ax15 .  Alternately,
     ~ ax-17 becomes unnecessary in principle with this axiom, but we lose the
     more powerful metalogic afforded by ~ ax-17 .  We retain ~ ax-15 here to
     provide completeness for systems with the simpler metalogic that results
     from omitting ~ ax-17 , which might be easier to study for some
     theoretical purposes. $)
  ax-15 $a |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x e. y -> A. z x e. y ) ) ) $.

  ${
    $d x z $.  $d y z $.
    $( Theorem to add distinct quantifier to atomic formula.  This theorem
       demonstrates the induction basis for ~ ax-17 considered as a
       metatheorem.) $)
    ax17el $p |- ( x e. y -> A. z x e. y ) $=
      ( weq wal wel wi ax-15 ax-16 pm2.61ii ) CADCECBDCEABFZKCEGABCHKCAIKCBIJ
      $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Version of ~ dveel2 using ~ ax-16 instead of ~ ax-17 . $)
    dveel2ALT $p |- ( -. A. x x = y -> ( z e. y -> A. x z e. y ) ) $=
      ( vw wel ax17el elequ2 dvelimfALT ) CDECBEABDCDAFCBDFDBCGH $.
      $( [10-May-2008] $)
  $}

  ${
    $d x u v $.  $d y u v $.  $d z u v $.  $d w u v $.
    $( Basis step for constructing a substitution instance of ~ ax-11o without
       using ~ ax-11o .  Atomic formula for equality predicate. $)
    ax11eq $p |- ( -. A. x x = y ->
               ( x = y -> ( z = w -> A. x ( x = y -> z = w ) ) ) ) $=
      ( vu vv weq wal wn wi wa 19.26 equid a1i ax-gen wb equequ1 equequ2
      sylan9bb a4s hba1 imbi2d albid imbi12d adantr mpbii exp32 sylbir
      ad2antll ax-12 impcom adantrr equtrr alimi syl6 sylbid adantll dral2
      ad2antrr mpbid imp biimprcd adantlr ad2antlr wex a9e ax-1 19.21aiv
      adantl dveeq2 im2anan9 sylibr syl 19.23adv mpi a1d 4cases ) ACGZAHZADGZAH
      ZABGZAHIZWBCDGZWBWDJZAHZJZJZJZVSWAKVRVTKZAHZWIVRVTALWKWCWBWGWKWCWBKZKAAGZ
      WBWMJZAHZJZWGWOWMWNAWMWBAMNONWKWPWGPWLWKWMWDWOWFWJWMWDPAVRWMCAGZVTWDACAQA
      DCRZSTZWKWNWEAWJAUAWKWMWDWBWSUBUCUDUEUFUGUHVSWAIZKZWCWBWGXAWLKVTWBVTJZAHZ
      JZWGWTWLXDVSWTWLKZVTBDGZXCWBVTXFPWTWCABDQUIXEXFXFAHZXCWTWCXFXGJZWBWCWTXHB
      DAUJUKULXFXBABDAUMUNUOUPUQVSXDWGPWTWLVSVTWDXCWFVRVTWDPAACDQTZXBWEACAVSVTW
      DWBXIUBURUDUSUTUGVSIZWAKZWCWBWGXKWLKWQWBWQJZAHZJZWGXJWLXNWAXJWLKZWQCBGZXM
      WBWQXPPXJWCABCRZUIXOXPXPAHZXMXJWCXPXRJZWBXJWCXSCBAUJVAULXPXLAWBWQXPXQVBUN
      UOUPVCWAXNWGPXJWLWAWQWDXMWFVTWQWDPAWRTZXLWEADAWAWQWDWBXTUBURUDVDUTUGXJWTK
      ZWHWCYAWGWBYAEDGZEVEWGEDVFYAYBWGEYAFCGZFVEYBWGJZFCVFYAYCYDFYAYCYBWGYAYCYB
      KZKZFEGZWBYGJZAHZJWGYGYHAYGWBVGVHYFYGWDYIWFYEYGWDPZYAYCYGCEGYBWDFCEQEDCRS
      ZVIYFYEAHZYIWFPYFYCAHZYBAHZKZYLYAYEYOXJYCYMWTYBYNACFVJADEVJVKVAYCYBALVLYL
      YHWEAYEAUAYLYGWDWBYEYJAYKTUBUCVMUDUFUGVNVOVNVOVPVPVQ $.
      $( [22-Jan-2007] $)
  $}

  ${
    $d x u v $.  $d y u v $.  $d z u v $.  $d w u v $.
    $( Basis step for constructing a substitution instance of ~ ax-11o without
       using ~ ax-11o .  Atomic formula for membership predicate. $)
    ax11el $p |- ( -. A. x x = y ->
               ( x = y -> ( z e. w -> A. x ( x = y -> z e. w ) ) ) ) $=
      ( vv vu weq wal wn wel wi wa 19.26 wb elequ1 elequ2 bitrd adantl ax-17
      dvelimfALT biimprcd alimi syl6 adantr sylbid sylan9bb a4s hba1 imbi2d
      albid imbi12d mpbid exp32 sylbir ad2antll ax-15 impcom adantrr adantll
      dral2 ad2antrr imp adantlr ad2antlr wex a9e ax-1 19.21aiv dveeq2
      im2anan9 sylibr syl mpbii 19.23adv mpi a1d 4cases ) ACGZAHZADGZAHZABGZAHI
      ZWBCDJZWBWDKZAHZKZKZKZVSWALVRVTLZAHZWIVRVTAMWKWCWBWGWKWCWBLZLAAJZWBWMKZAH
      ZKZWGWLWPWKWLWMBBJZWOWBWMWQNWCWBWMBAJWQABAOABBPQZRWCWQWOKWBWCWQWQAHWOEEJZ
      WQABEWSASWQESEBGWSBEJWQEBEOEBBPQTWQWNAWBWMWQWRUAUBUCUDUERWKWPWGNWLWKWMWDW
      OWFWJWMWDNAVRWMCAJZVTWDACAOADCPZUFUGZWKWNWEAWJAUHWKWMWDWBXBUIUJUKUDULUMUN
      VSWAIZLZWCWBWGXDWLLADJZWBXEKZAHZKZWGXCWLXHVSXCWLLZXEBDJZXGWBXEXJNXCWCABDO
      ZUOXIXJXJAHZXGXCWCXJXLKZWBWCXCXMBDAUPUQURXJXFAWBXEXJXKUAUBUCUEUSVSXHWGNXC
      WLVSXEWDXGWFVRXEWDNAACDOUGZXFWEACAVSXEWDWBXNUIUTUKVAULUMVSIZWALZWCWBWGXPW
      LLWTWBWTKZAHZKZWGXOWLXSWAXOWLLZWTCBJZXRWBWTYANXOWCABCPZUOXTYAYAAHZXRXOWCY
      AYCKZWBXOWCYDCBAUPVBURYAXQAWBWTYAYBUAUBUCUEVCWAXSWGNXOWLWAWTWDXRWFVTWTWDN
      AXAUGZXQWEADAWAWTWDWBYEUIUTUKVDULUMXOXCLZWHWCYFWGWBYFFDGZFVEWGFDVFYFYGWGF
      YFECGZEVEYGWGKZECVFYFYHYIEYFYHYGWGYFYHYGLZLZEFJZWBYLKZAHZKWGYLYMAYLWBVGVH
      YKYLWDYNWFYJYLWDNZYFYHYLCFJYGWDECFOFDCPUFZRYKYJAHZYNWFNYKYHAHZYGAHZLZYQYF
      YJYTXOYHYRXCYGYSACEVIADFVIVJVBYHYGAMVKYQYMWEAYJAUHYQYLWDWBYJYOAYPUGUIUJVL
      UKVMUMVNVOVNVOVPVPVQ $.
      $( [22-Jan-2007] $)
  $}

  ${
    ax11f.1 $e |- ( ph -> A. x ph ) $.
    $( Basis step for constructing a substitution instance of ~ ax-11o without
       using ~ ax-11o .  We can start with any formula ` ph ` in which ` x ` is
       not free. $)
    ax11f $p |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $=
      ( weq wal wn wi ax-1 19.21ai a1i12 ) BCEZBFGLALAHZBFHAMBDALIJK $.
      $( [21-Jan-2007] $)
  $}

  ${
    ax11indn.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( Induction step for constructing a substitution instance of ~ ax-11o
       without using ~ ax-11o .  Negation case. $)
    ax11indn $p |- ( -. A. x x = y ->
               ( x = y -> ( -. ph -> A. x ( x = y -> -. ph ) ) ) ) $=
      ( weq wal wn wi wa wex hbn1 con3 syl6 com23 19.21ad exanali syl5ib 19.8a
      syl5 exp3a ) BCEZBFGZUAAGZUAUCHZBFZUBUAUCIZBJZUEUFUBUAAHZBFZGZUEUGUBUJUDB
      UABKUHBKUBUAUJUCUBUAAUIHUJUCHDAUILMNOUAABPQUFBRST $.
      $( [21-Jan-2007] $)

    ${
      ax11indi.2 $e |- ( -. A. x x = y ->
                 ( x = y -> ( ps -> A. x ( x = y -> ps ) ) ) ) $.
      $( Induction step for constructing a substitution instance of ~ ax-11o
         without using ~ ax-11o .  Implication case. $)
      ax11indi $p |- ( -. A. x x = y ->
           ( x = y -> ( ( ph -> ps ) -> A. x ( x = y -> ( ph -> ps ) ) ) ) ) $=
        ( weq wal wn wi wa ax11indn imp pm2.21 imim2i alimi syl6 ax-1 jad ex )
        CDGZCHIZUAABJZUAUCJZCHZJUBUAKZABUEUFAIZUAUGJZCHZUEUBUAUGUIJACDELMUHUDCU
        GUCUAABNOPQUFBUABJZCHZUEUBUABUKJFMUJUDCBUCUABAROPQST $.
        $( [21-Jan-2007] $)
    $}
  $}

  ${
    ax11indalem.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( Lemma for ~ ax11inda2 and ~ ax11inda . $)
    ax11indalem $p |- ( -. A. y y = z -> ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) ) $=
      ( weq wal wn wi ax-1 a5i a1i biidd dral1 imbi2d dral2 3imtr4d alequcoms
      a1d adantr wa simplr ax-12 imp alequcom con3i syl2an adantlr hbnae hba1
      hban ax-4 sylan2 alimd syl11anc wb 19.21ai 19.21t syl albid ax-7 syl5bi
      ad2antrr syld exp31 pm2.61ian ) BDFBGZCDFCGZHZBCFZBGHZVJADGZVJVLIZBGZIZIZ
      IZVGVQVIVGVPVKVGVOVJVODBDBFDGZABGZVJVSIZBGZVLVNVSWAIVRAVTBVSVJJKLAADBVRAM
      NZVMVTDBBVRVLVSVJWBOPQRSSTVGHZVIUAZVKVJVOWDVKUAVJUAZVLVJAIZBGZDGZVNWEVKVJ
      DGZVLWHIWDVKVJUBWDVJWIVKWDVJWIVRHZDCFDGZHZVJWIIZWCVIWJWLWMBCDUCUDVRVGDBUE
      UFWKVHDCUEUFUGZUDUHVKWIUAAWGDVKWIDBCDUIVJDUJUKVKVJAWGIZWIVKVJWOEUDVJDULUM
      UNUOWDWHVNIVKVJWDWFDGZBGVNWHWDWPVMBWCVIBBDBUICDBUIUKWDWMDGWPVMUPWDWMDWCVI
      DBDDUICDDUIUKWNUQVJADURUSUTWFDBVAVBVCVDVEVF $.
      $( [24-Jan-2007] $)
  $}

  ${
    $d z y $.
    ax11inda2.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( A proof of ~ ax11inda2 that is slightly more direct. $)
    ax11inda2ALT $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( weq wal wn wi ax-1 a5i a1i biidd dral1 imbi2d dral2 3imtr4d alequcoms
      a1d wa simplr dveeq1 nalequcoms imp adantlr hbnae hba1 hban ax-4 sylan2
      alimd syl11anc wb 19.21ai 19.21t syl albid ax-7 syl5bi ad2antrr syld
      exp31 pm2.61i ) BDFBGZBCFZBGHZVEADGZVEVGIZBGZIZIZIVDVKVFVDVJVEVJDBDBFDGZA
      BGZVEVMIZBGZVGVIVMVOIVLAVNBVMVEJKLAADBVLAMNZVHVNDBBVLVGVMVEVPOPQRSSVDHZVF
      VEVJVQVFTVETZVGVEAIZBGZDGZVIVRVFVEDGZVGWAIVQVFVEUAVQVEWBVFVQVEWBVEWBIZDBD
      BCUBUCZUDUEVFWBTAVTDVFWBDBCDUFVEDUGUHVFVEAVTIZWBVFVEWEEUDVEDUIUJUKULVQWAV
      IIVFVEVQVSDGZBGVIWAVQWFVHBBDBUFVQWCDGWFVHUMVQWCDBDDUFWDUNVEADUOUPUQVSDBUR
      USUTVAVBVC $.
      $( [4-May-2007] $)

    $( Induction step for constructing a substitution instance of ~ ax-11o
       without using ~ ax-11o .  Quantification case.  When ` z ` and ` y ` are
       distinct, this theorem avoids the dummy variables needed by the more
       general ~ ax11inda . $)
    ax11inda2 $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( weq wal wn wi a16g ax-1 syl5 a1d ax11indalem pm2.61i ) CDFCGZBCFZBGHZQA
      DGZQSIZBGZIZIZIPUCRPUBQPTUASTCDBJSQKLMMABCDENO $.
      $( [24-Jan-2007] $)
  $}

  ${
    $d w ph $.  $d w x $.  $d w y $.  $d w z $.
    ax11inda.1 $e |- ( -. A. x x = w ->
               ( x = w -> ( ph -> A. x ( x = w -> ph ) ) ) ) $.
    $( Induction step for constructing a substitution instance of ~ ax-11o
       without using ~ ax-11o .  Quantification case.  (When ` z ` and ` y `
       are distinct, ~ ax11inda2 may be used instead to avoid the dummy
       variable ` w ` in the proof.) $)
    ax11inda $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( weq wal wn wi wex a9e wa ax11inda2 wb dveeq2 imp hba1 equequ2 a4s
      albid notbid syl adantl imbi1d imbi2d imbi12d mpbii ex 19.23adv mpi
      pm2.43i ) BCGZBHZIZUMADHZUMUPJZBHZJZJZUOECGZEKUOUTJZECLUOVAVBEUOVAVBUOVAM
      ZBEGZBHZIZVDUPVDUPJZBHZJZJZJVBABEDFNVCVFUOVJUTVCVABHZVFUOOUOVAVKBCEPQZVKV
      EUNVKVDUMBVABRZVAVDUMOZBECBSZTZUAUBUCVCVDUMVIUSVAVNUOVOUDVCVHURUPVCVKVHUR
      OVLVKVGUQBVMVKVDUMUPVPUEUAUCUFUGUGUHUIUJUKUL $.
      $( [24-Jan-2007] $)
  $}

  $( Part of a study related to ~ ax-12 .  The consequent introduces a new
     variable ` z ` .  There are no distinct variable restrictions. $)
  a12stdy1 $p |- ( A. x x = y -> E. x y = z ) $=
    ( weq wal wex a9e wn ax-10o con3d df-ex 3imtr4g mpi ) ABDAEZBCDZBFZOAFZBCGN
    OHZBEZHRAEZHPQNTSRABIJOBKOAKLM $.
    $( [14-Jan-2008] $)

  $( Part of a study related to ~ ax-12 .  The consequent is quantified with a
     different variable.  There are no distinct variable restrictions. $)
  a12stdy2 $p |- ( A. z ( z = x /\ x = y ) -> A. y y = x ) $=
    ( weq wa wal 19.26 ax-10o alequcom syl6 imp sylbi ) CADZABDZECFMCFZNCFZEBAD
    BFZMNCGOPQOPNAFQNCAHABIJKL $.
    $( [14-Jan-2008] $)

  $( Part of a study related to ~ ax-12 .  The consequent introduces two new
     variables.  There are no distinct variable restrictions. $)
  a12stdy3 $p |- ( A. z ( z = x /\ x = y ) -> A. v E. y x = w ) $=
    ( weq wa wal wex a12stdy2 hbae a12stdy1 alimi 3syl ) CAFABFGCHBAFBHZOEHADFB
    IZEHABCJBAEKOPEBADLMN $.
    $( [14-Jan-2008] $)

  $( Part of a study related to ~ ax-12 .  The second antecedent of ~ ax-12 is
     replaced.  There are no distinct variable restrictions. $)
  a12stdy4 $p |- ( -. A. z z = x -> ( A. y z = x ->
           ( x = y -> A. z x = y ) ) ) $=
    ( weq wal wn wi wa ax-10o alequcoms con3d impcom pm2.21d ex ax-12 a1dd
    pm2.61d ) CADZCEZFZCBDCEZRBEZABDZUCCEGZGZTUAUETUAHUBUDUATUBFUAUBSUBSGBCRBCI
    JKLMNTUAFUDUBABCOPQ $.
    $( [14-Jan-2008] $)

  $( Proof of first hypothesis of ~ a12study . $)
  a12lem1 $p |- ( -. A. z z = y ->
                  ( A. z ( z = x -> z = y ) -> x = y ) ) $=
    ( weq wal wn wi wb equequ1 imbi12d a4s dral2 equid a1bi biimpri syl6bi a1d
    wa 19.26 a4imt sylbir hbn1 hban hbth a1i ax-12 imp hbimd 19.21ai equtr
    ax-8 imim12d ax-gen sylancl mpii ex pm2.61i ) CADZCEZCBDZCEFZURUTGZCEZABDZG
    ZGUSVEVAUSVCAADZVDGZCEZVDVBVGCACURVBVGHCURURVFUTVDCAAICABIJKLVGVDCVDVGVFVDA
    MZNOKPQUSFZVAVEVJVARZVCVFVDVIVGVHGZCEZURVBVGGGZCEZVCVGGZVKVMVORVLVNRCEVPVLV
    NCSVBVGCATUAVKVLCVJVACURCUBUTCUBUCZVKVFVDCVQVFVFCEGVKVFCVIUDUEVJVAVDVDCEGAB
    CUFUGUHUIVNCURVFURUTVDCAAUJCABUKULUMUNUOUPUQ $.
    $( [15-Jan-2008] $)

  $( Proof of second hypothesis of ~ a12study . $)
  a12lem2 $p |- ( A. z ( z = x -> -. z = y ) -> -. x = y ) $=
    ( weq wn wi wal wa wex equcom imbi1i imnan bitri albii alnex equvini con3i
    sylbi ) CADZCBDZEZFZCGZACDZTHZCIZEZABDZEUCUEEZCGUGUBUICUBUDUAFUISUDUACAJKUD
    TLMNUECOMUHUFABCPQR $.
    $( [15-Jan-2008] $)

  ${
    a12study.1 $e |- ( -. A. z z = y ->
                    ( A. z ( z = x -> z = y ) -> x = y ) ) $.
    a12study.2 $e |- ( A. z ( z = x -> -. z = y ) -> -. x = y ) $.
    $( Rederivation of axiom ~ ax-12 from two shorter formulas, without using
       ~ ax-12 .  See ~ a12lem1 and ~ a12lem2 for the proofs of the hypotheses
       (using ~ ax-12 ).  This is the only known breakdown of ~ ax-12 into
       shorter formulas.  See ~ a12studyALT for an alternate proof.  Note that
       the proof depends on ~ ax-11o , whose proof ~ ax11o depends on ~ ax-12 ,
       meaning that we would have to replace ~ ax-11 with ~ ax-11o in an
       axiomatization that uses the hypotheses in place of ~ ax-12 .  Whether
       this can be avoided is an open problem. $)
    a12study $p |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x = y -> A. z x = y ) ) ) $=
      ( weq wal wn wa wex wi hbn1 hban hba1 ax-11o equid1 ax-8 mpi syl5 imp3a
      19.21ad sylan9 19.23ad ex imnan imim1i sylbir alimi syl con2i df-ex
      sylibr syl7 ) CAFZCGHZCBFZCGHZACFZUPIZCJZABFZCGZVAUOUQUTVBKUOUQIUSVBCUOUQ
      CUNCLUPCLZMVACNUOUSUNUPKZCGZUQVBUOURUPVEUOUNUPVEKURUPCAOURAAFUNAPACAQRSTU
      QVEVACVCVDCNDUAUBUCUDVAUSHZCGZHUTVGVAVGUNUPHZKZCGVAHVFVICVFURVHKVIURUPUEU
      NURVHUNCCFURCPCACQRUFUGUHEUIUJUSCUKULUM $.
      $( [15-Jan-2008] $)

    $( Alternate proof of ~ a12study , also without using ~ ax-12 . $)
    a12studyALT $p |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x = y -> A. z x = y ) ) ) $=
      ( weq wal wn wi wa hbn1 hban con3d wex hba1 ax-11o ax11indn a5i syl8
      imp3a annim syl5ibr 19.23ad exnal sylan9r hbnd notnot albii 3imtr4g ex )
      CAFZCGHZCBFZCGHZABFZUOCGZIULUNJZUOHZHZUSCGUOUPUQURCULUNCUKCKZUMCKLUNURUKU
      MIZCGZHZULURCGZUNVBUODMULVAHZCNVDVCULVEVDCUTURCOULUKUMHZJVDVEULUKVFVDULUK
      VFUKVFIZCGVDUMCAUMCAPQVGURCERSTUKUMUAUBUCVACUDUBUEUFUOUGZUOUSCVHUHUIUJ $.
      $( [17-Jan-2008] $)
  $}

