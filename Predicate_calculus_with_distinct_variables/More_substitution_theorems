$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        More substitution theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$( The theorems in this section make use of the $d statement. $)

  ${
    $d y z $. $d x y $.
    $( Lemma for ~ equsb3 . $)
    equsb3lem $p |- ( [ x / y ] y = z <-> x = z ) $=
      ( weq wsb wb equsb2 equequ1 sbimi ax-mp sbbi mpbi ax-17 sbf bitr3 ) BCDZB
      AEZACDZBAEZRRPFZBAEZSQFABDZBAEUABAGUBTBAABCHIJRPBAKLRBARBMNO $.
      $( [4-Dec-2005] $) $( [4-Dec-2005] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic wff.  (Contributed by Raph Levien and
       FL, 4-Dec-05.) $)
    equsb3 $p |- ( [ x / y ] y = z <-> x = z ) $=
      ( vw weq wsb equsb3lem sbbii ax-17 sbco2 3bitr3 ) BCEZBDFZDAFDCEZDAFLBAFA
      CEMNDADBCGHLBADLDIJADCGK $.
      $( [4-Dec-2005] $) $( [4-Dec-2005] $)
  $}

  ${
    $d w y z $.  $d w x $.
    $( Substitution applied to an atomic membership wff. $)
    elsb3 $p |- ( [ x / y ] y e. z <-> x e. z ) $=
      ( vw wel wsb wb weq equsb2 elequ1 sbimi ax-mp sbbi mpbi ax-17 sbf bitr3
      sbbii sbco2 3bitr3 ) BCEZBDFZDAFDCEZDAFZUABAFACEZUBUCDAUBUCBDFZUCUCUAGZBD
      FZUFUBGDBHZBDFUHBDIUIUGBDDBCJKLUCUABDMNUCBDUCBOPQRUABADUADOSUDUEDAFZUEUEU
      CGZDAFZUJUDGADHZDAFULDAIUMUKDAADCJKLUEUCDAMNUEDAUEDOPQT $.
      $( [7-Nov-2006] $) $( [7-Nov-2006] $)
  $}

  ${
    $d x y $.
    $( ` x ` is not free in ` [ y / x ] ph ` when ` x ` and ` y ` are
       distinct. $)
    hbs1 $p |- ( [ y / x ] ph -> A. x [ y / x ] ph ) $=
      ( weq wal wsb wi ax16 hbsb2 pm2.61i ) BCDBEABCFZKBEGKBCHABCIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d y z $.
    hbsb.1 $e |- ( ph -> A. z ph ) $.
    $( If ` z ` is not free in ` ph ` , it is not free in ` [ y / x ] ph `
       when ` y ` and ` z ` are distinct. $)
    hbsb $p |- ( [ y / x ] ph -> A. z [ y / x ] ph ) $=
      ( weq wal wsb wi ax16 hbsb4 pm2.61i ) DCFDGABCHZMDGIMDCJABCDEKL $.
      $( [12-Aug-1993] $)
  $}

  ${
    $d x z $.  $d x w $.  $d y z $.
    $( Commutativity law for substitution.  Used in proof of Theorem 9.7 of
       [Megill] p. 449 (p. 16 of the preprint). $)
    sbcom2 $p |- ( [ w / z ] [ y / x ] ph <-> [ y / x ] [ w / z ] ph ) $=
      ( weq wal wsb wb wn wa wi alcom bi2.04 albii 19.21v bitr 3bitr3 a1i sb4b
      imbi2d albidv sylan9bbr sylan9bb 3bitr4d ex hbae sbequ12 a4s sbbid
      bitr3d pm2.61ii ) BCFZBGZDEFZDGZABCHZDEHZADEHZBCHZIZUNJZUPJZVAVBVCKZUOUMA
      LZBGZLZDGZUMUOALZDGZLZBGZURUTVHVLIVDUMVILZBGZDGVMDGZBGVHVLVMDBMVNVGDVNUOV
      ELZBGVGVMVPBUMUOANOUOVEBPQOVOVKBUMVIDPORSVCURUOUQLZDGVBVHUQDETVBVQVGDVBUQ
      VFUOABCTUAUBUCVBUTUMUSLZBGVCVLUSBCTVCVRVKBVCUSVJUMADETUAUBUDUEUFUNUSURUTU
      NAUQDEBCDUGUMAUQIBABCUHUIUJUMUSUTIBUSBCUHUIUKUPUQURUTUOUQURIDUQDEUHUIUPAU
      SBCDEBUGUOAUSIDADEUHUIUJUKUL $.
      $( [27-May-1997] $)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution.  Similar to Theorem 6.1 of [Quine]
       p. 40. $)
    sb5 $p |- ( [ y / x ] ph <-> E. x ( x = y /\ ph ) ) $=
      ( weq wa wex wsb hbs1 sbequ12 equsex bicomi ) BCDAEBFABCGZALBCABCHABCIJK
      $.
      $( [18-Aug-1993] $)

    $( Equivalence for substitution.  Compare Theorem 6.2 of [Quine] p. 40. $)
    sb6 $p |- ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) $=
      ( weq wi wal wsb hbs1 sbequ12 equsal bicomi ) BCDAEBFABCGZALBCABCHABCIJK
      $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y z $.  $d w y $.
    $( Equivalence for double substitution. $)
    2sb5 $p |- ( [ z / x ] [ w / y ] ph <->
               E. x E. y ( ( x = z /\ y = w ) /\ ph ) ) $=
      ( wsb weq wa wex sb5 19.42v anass exbii anbi2i 3bitr4r bitr ) ACEFZBDFBDG
      ZQHZBIRCEGZHAHZCIZBIQBDJSUBBRTAHZHZCIRUCCIZHUBSRUCCKUAUDCRTALMQUERACEJNOM
      P $.
      $( [25-Mar-2005] $) $( [3-Feb-2005] $)

    $( Equivalence for double substitution. $)
    2sb6 $p |- ( [ z / x ] [ w / y ] ph <->
               A. x A. y ( ( x = z /\ y = w ) -> ph ) ) $=
      ( wsb weq wi wal wa sb6 19.21v impexp albii imbi2i 3bitr4r bitr ) ACEFZBD
      FBDGZRHZBISCEGZJAHZCIZBIRBDKTUCBSUAAHZHZCISUDCIZHUCTSUDCLUBUECSUAAMNRUFSA
      CEKOPNQ $.
      $( [12-Feb-2005] $) $( [3-Feb-2005] $)
  $}

  ${
    $d x y $.
    $( Equivalence for substitution. $)
    sb6a $p |- ( [ y / x ] ph <-> A. x ( x = y -> [ x / y ] ph ) ) $=
      ( wsb weq wi wal sb6 wb sbequ12 equcoms pm5.74i albii bitr ) ABCDBCEZAFZB
      GOACBDZFZBGABCHPRBOAQAQICBACBJKLMN $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x w $.  $d y z $.  $d z w $.
    2sb5rf.1 $e |- ( ph -> A. z ph ) $.
    2sb5rf.2 $e |- ( ph -> A. w ph ) $.
    $( Reversed double substitution. $)
    2sb5rf $p |- ( ph <->
                E. z E. w ( ( z = x /\ w = y ) /\ [ z / x ] [ w / y ] ph ) ) $=
      ( weq wsb wa wex sb5rf 19.42v sbcom2 anbi2i anass bitr exbii hbsb
      3bitr4r ) ADBHZABDIZJZDKUAECHZJZACEIBDIZJZEKZDKABDFLUCUHDUAUDUBCEIZJZJZEK
      UAUJEKZJUHUCUAUJEMUGUKEUGUEUIJUKUFUIUEACEBDNOUAUDUIPQRUBULUAUBCEABDEGSLOT
      RQ $.
      $( [16-May-2005] $) $( [3-Feb-2005] $)

    $( Reversed double substitution. $)
    2sb6rf $p |- ( ph <->
                A. z A. w ( ( z = x /\ w = y ) -> [ z / x ] [ w / y ] ph ) ) $=
      ( weq wsb wi wal wa sb6rf 19.21v sbcom2 imbi2i impexp bitr albii hbsb
      3bitr4r ) ADBHZABDIZJZDKUBECHZLZACEIBDIZJZEKZDKABDFMUDUIDUBUEUCCEIZJZJZEK
      UBUKEKZJUIUDUBUKENUHULEUHUFUJJULUGUJUFACEBDOPUBUEUJQRSUCUMUBUCCEABDEGTMPU
      ASR $.
      $( [6-Jun-2005] $) $( [3-Feb-2005] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z ph $.
    $( An alternate definition of proper substitution ~ df-sb . By
       introducing a dummy variable ` z ` in the definiens, we are able to
       eliminate any distinct variable restrictions among the variables
       ` x ` , ` y ` , and ` ph ` of the definiendum.  No distinct variable
       conflicts arise because ` z ` effectively insulates ` x ` from ` y ` .
       To achieve this, we use a chain of two substitutions in the form of
       ~ sb5 , first ` z ` for ` x ` then ` y ` for ` z ` . Compare Definition
       2.1'' of [Quine] p. 17, which is obtained from this theorem by applying
       ~ df-clab .  Theorem ~ sb7f provides a version where ` ph ` and ` z `
       don't have to be distinct.  $)
    sb7 $p |- ( [ y / x ] ph <-> E. z ( z = y /\ E. x ( x = z /\ ph ) ) ) $=
      ( wsb weq wa wex sb5 sbbii ax-17 sbco2 3bitr3 ) ABDEZDCEBDFAGBHZDCEABCEDC
      FOGDHNODCABDIJABCDADKLODCIM $.
      $( [29-Jan-2004] $) $( [28-Jan-2004] $)
  $}

  ${
    $d w x z $.  $d w y z $.  $d w ph $.
    sb7f.1 $e |- ( ph -> A. z ph ) $.
    $( This version of ~ sb7 does not require that ` ph ` and ` z ` be
       distinct.  This permits it to be used as a definition for substitution
       in a formalization that omits the logically redundant axiom ~ ax-17
       i.e. that doesn't have the concept of a variable not occurring in a
       wff.   ( ~ df-sb is also suitable, but its mixing of free and bound
       variables is distasteful to some logicians.) $)
    sb7f $p |- ( [ y / x ] ph <->
               E. z ( z = y /\ E. x ( x = z /\ ph ) ) ) $=
      ( vw wsb weq wa wex sb7 ax-17 hban hbex equequ1 equequ2 anbi1d exbidv
      anbi12d cbvex bitr ) ABCGFCHZBFHZAIZBJZIZFJDCHZBDHZAIZBJZIZDJABCFKUFUKFDU
      BUEDUBDLUDDBUCADUCDLEMNMUKFLFDHZUBUGUEUJFDCOULUDUIBULUCUHAFDBPQRSTUA $.
      $( [4-Aug-2006] $) $( [26-Jul-2006] $)
  $}

  ${
    $d x y $.
    sb10f.1 $e |- ( ph -> A. x ph ) $.
    $( Hao Wang's identity axiom P6 in Irving Copi, _Symbolic Logic_ (5th ed.,
       1979), p. 328.  In traditional predicate calculus, this is a sole
       axiom for identity from which the usual ones can be derived. $)
    sb10f $p |- ( [ y / z ] ph <-> E. x ( x = y /\ [ x / z ] ph ) ) $=
      ( weq wsb wa wex hbsb sbequ equsex bicomi ) BCFADBGZHBIADCGZNOBCADCBEJABC
      DKLM $.
      $( [9-May-2005] $) $( [9-May-2005] $)
  $}

  ${
    $d x ph $.
    $( An identity law for substitution.  Used in proof of Theorem 9.7 of
       [Megill] p. 449 (p. 16 of the preprint). $)
    sbid2v $p |- ( [ y / x ] [ x / y ] ph <-> ph ) $=
      ( ax-17 sbid2 ) ABCABDE $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x ph $.
    $( Elimination of substitution. $)
    sbelx $p |- ( ph <-> E. x ( x = y /\ [ x / y ] ph ) ) $=
      ( wsb weq wa wex sbid2v sb5 bitr3 ) AACBDZBCDBCEKFBGABCHKBCIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $( Note:  A more general case could also be proved with
       "$d x z $.  $d y w $.  $d x ph $.  $d y ph $.", but with more
       difficulty. $)
    $d x y z $.  $d w y $.  $d x y ph $.
    $( Elimination of double substitution. $)
    sbel2x $p |- ( ph <-> E. x E. y ( ( x = z /\ y = w ) /\
                     [ y / w ] [ x / z ] ph ) ) $=
      ( weq wsb wa wex sbelx anbi2i exbii exdistr 3bitr4 anass 2exbii bitr4 )
      ABDFZCEFZADBGZECGZHZHZCIBIZRSHUAHZCIBIRTHZBIRUBCIZHZBIAUDUFUHBTUGRTCEJKLA
      BDJRUBBCMNUEUCBCRSUAOPQ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.
    $( A theorem used in elimination of disjoint variable restriction on ` x `
       and ` y ` by replacing it with a distinctor ` -. A. x x = z ` . $)
    sbal1 $p |- ( -. A. x x = z ->
             ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) ) $=
      ( weq wal wn wsb wb wi sbequ12 a4s dral2 bitr3d a1d wa hba1 hbsb4 ax-4
      sbimi 19.20i syl6 adantl sb4 19.20ii hbnaes ax-7 ax16 ax-12 pm2.61i
      19.20 syl9 sb2 sylan9 impbid ex ) CDEZCFZBDEBFGZABFZCDHZACDHZBFZIZJURVDUS
      URUTVAVCUQUTVAICUTCDKLAVBCDBUQAVBICACDKLMNOURGZUSVDVEUSPVAVCUSVAVCJVEUSVA
      VABFVCUTCDBABQRVAVBBUTACDABSTUAUBUCVEVCUQAJZBFZCFZUSVAVEVCVFCFZBFZVHVCVJJ
      CDBVEVBVIBACDUDUEUFVFBCUGUBVHVAJBDCUSCFVHUQUTJZCFVAUSVGVKCUSUQUQBFZVGUTBC
      EBFZUSUQVLJZJVMVNUSUQBCUHOCDBUIUJUQABUKULUEUTCDUMUBUFUNUOUPUJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x z $.
    $( Move universal quantifier in and out of substitution. $)
    sbal $p |- ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) $=
      ( weq wal wsb wb a16gb sbimi sbequ5 sbbi 3imtr3 bitr3d sbal1 pm2.61i ) BD
      EBFZABFZCDGZACDGZBFZHQTSUAQCDGARHZCDGQTSHQUBCDABDBIJBDCDKARCDLMTBDBINABCD
      OP $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y $.  $d x z $.
    $( Move existential quantifier in and out of substitution. $)
    sbex $p |- ( [ z / y ] E. x ph <-> E. x [ z / y ] ph ) $=
      ( wn wal wsb wex sbn sbal albii bitr negbii df-ex sbbii 3bitr4 ) AEZBFZEZ
      CDGZACDGZEZBFZEZABHZCDGUABHTRCDGZEUDRCDIUFUCUFQCDGZBFUCQBCDJUGUBBACDIKLML
      UESCDABNOUABNP $.
      $( [27-Sep-2003] $)
  $}

  ${
    $d x z $.  $d y z $.
    sbalv.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Quantify with new variable inside substitution. $)
    sbalv $p |- ( [ y / x ] A. z ph <-> A. z ps ) $=
      ( wal wsb sbal albii bitr ) AEGCDHACDHZEGBEGAECDILBEFJK $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( An equivalent expression for existence. $)
    exsb $p |- ( E. x ph <-> E. y A. x ( x = y -> ph ) ) $=
      ( wex wsb weq wi wal ax-17 sb8e sb6 exbii bitr ) ABDABCEZCDBCFAGBHZCDABCA
      CIJNOCABCKLM $.
      $( [12-Feb-2005] $) $( [2-Feb-2005] $)
  $}

  ${
    $d x y z $.  $d y w $.  $d z w ph $.
    $( An equivalent expression for double existence. $)
    2exsb $p |- ( E. x E. y ph <->
                  E. z E. w A. x A. y ( ( x = z /\ y = w ) -> ph ) ) $=
      ( wex weq wi wal wa exsb exbii excom bitr impexp albii 19.21v bitr2
      3bitr ) ACFZBFZCEGZAHZCIZBFZEFZBDGZUBJAHZCIZBIZDFZEFUJEFDFUAUDEFZBFUFTULB
      ACEKLUDBEMNUEUKEUEUGUDHZBIZDFUKUDBDKUNUJDUMUIBUIUGUCHZCIUMUHUOCUGUBAOPUGU
      CCQRPLNLUJEDMS $.
      $( [12-Feb-2005] $) $( [2-Feb-2005] $)
  $}

  ${
    $d z ps $.
    dvelim.1 $e |- ( ph -> A. x ph ) $.
    dvelim.2 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( This theorem can be used to eliminate a distinct variable restriction on
       ` x ` and ` z ` and replace it with the "distinctor" ` -. A. x x = y `
       as an antecedent.  ` ph ` is considered to contain ` z ` , and ` ps ` to
       have ` z ` replaced with ` y ` , and we don't require that ` x ` and
       ` y ` be distinct (if they aren't, the distinctor will become false and
       "protect" the consequent).

       To obtain a closed theorem form of this inference, conjoin the
       hypotheses and apply ~ dvelimdf . $)
    dvelim $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( ax-17 dvelimf ) ABCDEFBEHGI $.
      $( [23-Nov-1994] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveeq1 $p |- ( -. A. x x = y -> ( y = z -> A. x y = z ) ) $=
      ( vw weq ax-17 equequ1 dvelimf2 ) DCEZBCEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveel1 $p |- ( -. A. x x = y -> ( y e. z -> A. x y e. z ) ) $=
      ( vw wel ax-17 elequ1 dvelimf2 ) DCEZBCEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d w z x $.  $d w y $.
    $( Quantifier introduction when one pair of variables is distinct. $)
    dveel2 $p |- ( -. A. x x = y -> ( z e. y -> A. x z e. y ) ) $=
      ( vw wel ax-17 elequ2 dvelimf2 ) CDEZCBEZABDIAFJDFDBCGH $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d z y $.  $d z x $.
    $( Move quantifier in and out of substitution. $)
    sbal2 $p |- ( -. A. x x = y ->
             ( [ z / y ] A. x ph <-> A. x [ z / y ] ph ) ) $=
      ( weq wal wn wi wsb hbnae wb dveeq1 19.20i hbnaes 19.21g syl albid alcom
      syl5rbbr sb6 albii 3bitr4g ) BCEBFGZCDEZABFZHZCFZUDAHZCFZBFZUECDIACDIZBFU
      CUHBFZCFUGUJUCULUFCBCCJUCUDUDBFHZBFZULUFKUNBCBUCUMBBCDLMNUDABOPQUHCBRSUEC
      DTUKUIBACDTUAUB $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d w y $.  $d w z $.  $d w x $.  $( ` w ` is dummy. $)
    $( Axiom ~ ax-15 is redundant if we assume ~ ax-17 .  Remark 9.6 in
       [Megill] p. 448 (p. 16 of the preprint), regarding axiom scheme C14'.

       Note that ` w ` is a dummy variable introduced in the proof.  On the
       web page, it is implicitly assumed to be distinct from all other
       variables.  (This is made explicit in the database file set.mm).  Its
       purpose is to satisfy the distinct variable requirements of ~ dveel2 and
       ~ dvelim .  By the end of the proof it has vanished, and the final
       theorem has no distinct variable requirements.  $)
    ax15 $p |- ( -. A. z z = x -> ( -. A. z z = y ->
              ( x e. y -> A. z x e. y ) ) ) $=
      ( vw weq wal wn wel wi cv wcel hbn1 dveel2 hbim1 ax-17 wceq elequ1
      imbi2d dvelimf2 19.21 syl6ib pm2.86d ) CAECFGZCBEZCFGZABHZUFCFZUCUEUFIZUH
      CFUEUGIUEDJZBJKZIUHCADUEUJCUDCLZCBDMNUHDOUIAJPUJUFUEDABQRSUEUFCUKTUAUB $.
      $( [29-Jun-1995] $)
  $}

  ${
    $d x u v $.  $d y u v $.  $d z u v $.  $d w u v $.
    $( Basis step for constructing a substitution instance of ~ ax11o without
       using ~ ax11o .  Atomic formula for equality predicate. $)
    ax11eq $p |- ( -. A. x x = y ->
               ( x = y -> ( z = w -> A. x ( x = y -> z = w ) ) ) ) $=
      ( vu vv weq wal wn wi wa 19.26 equid a1i ax-gen wb equequ1 equequ2
      sylan9bb a4s hba1 imbi2d albid imbi12d adantr mpbii exp32 sylbir
      ad2antll ax-12 impcom adantrr equtrr 19.20i syl6 sylbid adantll dral2
      ad2antrr mpbid imp biimprcd adantlr ad2antlr wex a9e ax-1 19.21aiv
      adantl dveeq2 im2anan9 sylibr syl 19.23adv mpi a1d 4cases ) ACGZAHZADGZAH
      ZABGZAHIZWBCDGZWBWDJZAHZJZJZJZVSWAKVRVTKZAHZWIVRVTALWKWCWBWGWKWCWBKZKAAGZ
      WBWMJZAHZJZWGWOWMWNAWMWBAMNONWKWPWGPWLWKWMWDWOWFWJWMWDPAVRWMCAGZVTWDACAQA
      DCRZSTZWKWNWEAWJAUAWKWMWDWBWSUBUCUDUEUFUGUHVSWAIZKZWCWBWGXAWLKVTWBVTJZAHZ
      JZWGWTWLXDVSWTWLKZVTBDGZXCWBVTXFPWTWCABDQUIXEXFXFAHZXCWTWCXFXGJZWBWCWTXHB
      DAUJUKULXFXBABDAUMUNUOUPUQVSXDWGPWTWLVSVTWDXCWFVRVTWDPAACDQTZXBWEACAVSVTW
      DWBXIUBURUDUSUTUGVSIZWAKZWCWBWGXKWLKWQWBWQJZAHZJZWGXJWLXNWAXJWLKZWQCBGZXM
      WBWQXPPXJWCABCRZUIXOXPXPAHZXMXJWCXPXRJZWBXJWCXSCBAUJVAULXPXLAWBWQXPXQVBUN
      UOUPVCWAXNWGPXJWLWAWQWDXMWFVTWQWDPAWRTZXLWEADAWAWQWDWBXTUBURUDVDUTUGXJWTK
      ZWHWCYAWGWBYAEDGZEVEWGEDVFYAYBWGEYAFCGZFVEYBWGJZFCVFYAYCYDFYAYCYBWGYAYCYB
      KZKZFEGZWBYGJZAHZJWGYGYHAYGWBVGVHYFYGWDYIWFYEYGWDPZYAYCYGCEGYBWDFCEQEDCRS
      ZVIYFYEAHZYIWFPYFYCAHZYBAHZKZYLYAYEYOXJYCYMWTYBYNACFVJADEVJVKVAYCYBALVLYL
      YHWEAYEAUAYLYGWDWBYEYJAYKTUBUCVMUDUFUGVNVOVNVOVPVPVQ $.
      $( [22-Jan-2007] $) $( [22-Jan-2007] $)
  $}

  ${
    $d x u v $.  $d y u v $.  $d z u v $.  $d w u v $.
    $( Basis step for constructing a substitution instance of ~ ax11o
       without using ~ ax11o .  Atomic formula for membership predicate. $)
    ax11el $p |- ( -. A. x x = y ->
               ( x = y -> ( z e. w -> A. x ( x = y -> z e. w ) ) ) ) $=
      ( vv vu weq wal wn wel wi wa 19.26 wb elequ1 elequ2 bitrd adantl ax-17
      dvelimf2 biimprcd 19.20i syl6 adantr sylbid sylan9bb a4s hba1 imbi2d
      albid imbi12d mpbid exp32 sylbir ad2antll ax15 impcom adantrr adantll
      dral2 ad2antrr imp adantlr ad2antlr wex a9e ax-1 19.21aiv dveeq2
      im2anan9 sylibr syl mpbii 19.23adv mpi a1d 4cases ) ACGZAHZADGZAHZABGZAHI
      ZWBCDJZWBWDKZAHZKZKZKZVSWALVRVTLZAHZWIVRVTAMWKWCWBWGWKWCWBLZLAAJZWBWMKZAH
      ZKZWGWLWPWKWLWMBBJZWOWBWMWQNWCWBWMBAJWQABAOABBPQZRWCWQWOKWBWCWQWQAHWOEEJZ
      WQABEWSASWQESEBGWSBEJWQEBEOEBBPQTWQWNAWBWMWQWRUAUBUCUDUERWKWPWGNWLWKWMWDW
      OWFWJWMWDNAVRWMCAJZVTWDACAOADCPZUFUGZWKWNWEAWJAUHWKWMWDWBXBUIUJUKUDULUMUN
      VSWAIZLZWCWBWGXDWLLADJZWBXEKZAHZKZWGXCWLXHVSXCWLLZXEBDJZXGWBXEXJNXCWCABDO
      ZUOXIXJXJAHZXGXCWCXJXLKZWBWCXCXMBDAUPUQURXJXFAWBXEXJXKUAUBUCUEUSVSXHWGNXC
      WLVSXEWDXGWFVRXEWDNAACDOUGZXFWEACAVSXEWDWBXNUIUTUKVAULUMVSIZWALZWCWBWGXPW
      LLWTWBWTKZAHZKZWGXOWLXSWAXOWLLZWTCBJZXRWBWTYANXOWCABCPZUOXTYAYAAHZXRXOWCY
      AYCKZWBXOWCYDCBAUPVBURYAXQAWBWTYAYBUAUBUCUEVCWAXSWGNXOWLWAWTWDXRWFVTWTWDN
      AXAUGZXQWEADAWAWTWDWBYEUIUTUKVDULUMXOXCLZWHWCYFWGWBYFFDGZFVEWGFDVFYFYGWGF
      YFECGZEVEYGWGKZECVFYFYHYIEYFYHYGWGYFYHYGLZLZEFJZWBYLKZAHZKWGYLYMAYLWBVGVH
      YKYLWDYNWFYJYLWDNZYFYHYLCFJYGWDECFOFDCPUFZRYKYJAHZYNWFNYKYHAHZYGAHZLZYQYF
      YJYTXOYHYRXCYGYSACEVIADFVIVJVBYHYGAMVKYQYMWEAYJAUHYQYLWDWBYJYOAYPUGUIUJVL
      UKVMUMVNVOVNVOVPVPVQ $.
      $( [22-Jan-2007] $) $( [22-Jan-2007] $)
  $}

  ${
    ax11f.1 $e |- ( ph -> A. x ph ) $.
    $( Basis step for constructing a substitution instance of ~ ax11o without
       using ~ ax11o .  We can start with any formula ` ph ` in which ` x ` is
       not free. $)
    ax11f $p |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $=
      ( weq wi wal wn ax-1 19.21ai a1i ) BCEZALAFZBGFZFLBGHNLAMBDALIJKK $.
      $( [22-Jan-2007] $) $( [21-Jan-2007] $)
  $}

  ${
    ax11indn.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( Induction step for constructing a substitution instance of ~ ax11o
       without using ~ ax11o .  Negation case. $)
    ax11indn $p |- ( -. A. x x = y ->
               ( x = y -> ( -. ph -> A. x ( x = y -> -. ph ) ) ) ) $=
      ( weq wal wn wi wa wex hbn1 con3 syl6 com23 19.21ad exanali syl5ib 19.8a
      syl5 exp3a ) BCEZBFGZUAAGZUAUCHZBFZUBUAUCIZBJZUEUFUBUAAHZBFZGZUEUGUBUJUDB
      UABKUHBKUBUAUJUCUBUAAUIHUJUCHDAUILMNOUAABPQUFBRST $.
      $( [21-Jan-2007] $) $( [21-Jan-2007] $)

    ${
      ax11indi.2 $e |- ( -. A. x x = y ->
                 ( x = y -> ( ps -> A. x ( x = y -> ps ) ) ) ) $.
      $( Induction step for constructing a substitution instance of ~ ax11o
         without using ~ ax11o .  Implication case. $)
      ax11indi $p |- ( -. A. x x = y ->
           ( x = y -> ( ( ph -> ps ) -> A. x ( x = y -> ( ph -> ps ) ) ) ) ) $=
        ( weq wal wn wi wa wo ax11indn imp pm2.21 imim2i 19.20i syl6 ax-1 jaod
        imor syl5ib ex ) CDGZCHIZUDABJZUDUFJZCHZJUEUDKZAIZBLUHUFUIUJUHBUIUJUDUJ
        JZCHZUHUEUDUJULJACDEMNUKUGCUJUFUDABOPQRUIBUDBJZCHZUHUEUDBUNJFNUMUGCBUFU
        DBASPQRTABUAUBUC $.
        $( [22-Jan-2007] $) $( [21-Jan-2007] $)
    $}
  $}

  ${
    ax11indalem.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( Lemma for ~ ax11inda2 and ~ ax11inda . $)
    ax11indalem $p |- ( -. A. y y = z  -> ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) ) $=
      ( weq wal wn wi ax-1 a5i a1i pm4.2i dral1 imbi2d dral2 3imtr4d alequcoms
      a1d adantr wa hbnae hba1 hban imp ax-4 sylan2 19.20d simplr ax-12
      alequcom con3i syl2an adantlr sylanc wb 19.21ai 19.21g syl albid ax-7
      syl5bi ad2antrr syld exp31 pm2.61ian ) BDFBGZCDFCGZHZBCFZBGHZVJADGZVJVLIZ
      BGZIZIZIZVGVQVIVGVPVKVGVOVJVODBDBFDGZABGZVJVSIZBGZVLVNVSWAIVRAVTBVSVJJKLA
      ADBVRAMNZVMVTDBBVRVLVSVJWBOPQRSSTVGHZVIUAZVKVJVOWDVKUAVJUAZVLVJAIZBGZDGZV
      NVKVJDGZVLWHIWEVKWIUAAWGDVKWIDBCDUBVJDUCUDVKVJAWGIZWIVKVJWJEUEVJDUFUGUHWD
      VKVJUIWDVJWIVKWDVJWIVRHZDCFDGZHZVJWIIZWCVIWKWMWNBCDUJUEVRVGDBUKULWLVHDCUK
      ULUMZUEUNUOWDWHVNIVKVJWDWFDGZBGVNWHWDWPVMBWCVIBBDBUBCDBUBUDWDWNDGWPVMUPWD
      WNDWCVIDBDDUBCDDUBUDWOUQVJADURUSUTWFDBVAVBVCVDVEVF $.
      $( [14-Mar-2007] $) $( [24-Jan-2007] $)
  $}

  ${
    $d z y $.
    ax11inda2.1 $e |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.
    $( A direct proof of ~ ax11inda2 that is slightly shorter than the
       ~ ax11indalem used for ~ ax11inda2 . $)
    ax11inda2ALT $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( cv wceq wal wn wi ax-1 a5i a1i pm4.2i dral1 imbi2d dral2 3imtr4d
      alequcoms a1d wa hbnae hba1 hban imp ax-4 sylan2 19.20d simplr alequcom
      con3i dveeq1 syl adantlr sylanc hbn1 wb 19.21ai 19.21g albid ax-7 syl5bi
      ad2antrr syld exp31 pm2.61i ) BFZDFZGZBHZVGCFGZBHIZVKADHZVKVMJZBHZJZJZJVJ
      VQVLVJVPVKVPDBVHVGGDHZABHZVKVSJZBHZVMVOVSWAJVRAVTBVSVKKLMAADBVRANOZVNVTDB
      BVRVMVSVKWBPQRSTTVJIZVLVKVPWCVLUAVKUAZVMVKAJZBHZDHZVOVLVKDHZVMWGJWDVLWHUA
      AWFDVLWHDBCDUBVKDUCUDVLVKAWFJZWHVLVKWIEUEVKDUFUGUHWCVLVKUIWCVKWHVLWCVKWHW
      CVRIVKWHJZVRVJDBUJUKDBCULUMZUEUNUOWCWGVOJVLVKWCWEDHZBHVOWGWCWLVNBVIBUPWCW
      JDHWLVNUQWCWJDBDDUBWKURVKADUSUMUTWEDBVAVBVCVDVEVF $.
      $( [?] $) $( [4-May-2007] $)

    $( Induction step for constructing a substitution instance of ~ ax11o
       without using ~ ax11o .  Quantification case.  When ` z ` and ` y ` are
       distinct, this theorem avoids the dummy variables needed by the more
       general ~ ax11inda . $)
    ax11inda2 $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( weq wal wn wi a16g ax-1 syl5 a1d ax11indalem pm2.61i ) CDFCGZBCFZBGHZQA
      DGZQSIZBGZIZIZIPUCRPUBQPTUASTCDBJSQKLMMABCDENO $.
      $( [14-Mar-2007] $) $( [24-Jan-2007] $)
  $}

  ${
    $d w ph $.  $d w x $.  $d w y $.  $d w z $.
    ax11inda.1 $e |- ( -. A. x x = w ->
               ( x = w -> ( ph -> A. x ( x = w -> ph ) ) ) ) $.
    $( Induction step for constructing a substitution instance of ~ ax11o
       without using ~ ax11o .  Quantification case.  (When ` z ` and ` y ` are
       distinct, ~ ax11inda2 may be used instead to avoid the dummy variable
       ` w ` in the proof.) $)
    ax11inda $p |- ( -. A. x x = y ->
               ( x = y -> ( A. z ph -> A. x ( x = y -> A. z ph ) ) ) ) $=
      ( weq wal wn wi wex a9e wa ax11inda2 wb dveeq2 imp hba1 equequ2 a4s
      albid negbid syl adantl imbi1d imbi2d imbi12d mpbii ex 19.23adv mpi
      pm2.43i ) BCGZBHZIZUMADHZUMUPJZBHZJZJZUOECGZEKUOUTJZECLUOVAVBEUOVAVBUOVAM
      ZBEGZBHZIZVDUPVDUPJZBHZJZJZJVBABEDFNVCVFUOVJUTVCVABHZVFUOOUOVAVKBCEPQZVKV
      EUNVKVDUMBVABRZVAVDUMOZBECBSZTZUAUBUCVCVDUMVIUSVAVNUOVOUDVCVHURUPVCVKVHUR
      OVLVKVGUQBVMVKVDUMUPVPUEUAUCUFUGUGUHUIUJUKUL $.
      $( [14-Mar-2007] $) $( [24-Jan-2007] $)
  $}


