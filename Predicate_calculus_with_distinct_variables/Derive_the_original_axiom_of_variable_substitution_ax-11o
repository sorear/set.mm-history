$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Derive the original axiom of variable substitution ax-11o
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z $.  $d y z $.  $d z ph $.
    $( Derivation of set.mm's original ~ ax-11o from the shorter ~ ax-11 that
       has replaced it.  Axiom scheme C15' in [Megill] p. 448 (p. 16 of the
       preprint).  It is based on Lemma 16 of [Tarski] p. 70 and Axiom C8 of
       [Monk2] p. 105, from which it can be proved by cases.  To understand
       this theorem more easily, think of " ` -. A. x x = y -> ` ..." as
       informally meaning "if ` x ` and ` y ` are distinct variables then..."
       The antecedent becomes false if the same variable is substituted for
       ` x ` and ` y ` , ensuring the theorem is sound whenever this is the
       case.  In some later theorems we call an antecedent of the form
       ` -. A. x x = y ` a "distinctor".

       An open problem is whether this theorem can be proved without relying on
       ~ ax-16 or ~ ax-17 .

       Theorem ~ ax11 shows the reverse derivation of ~ ax-11 from
       ~ ax-11o . $)
    ax11o $p |- ( -. A. x x = y ->
               ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $=
      ( vz ax-11 ax11a2 ) ABCDABDEF $.
      $( [5-Feb-2007] $) $( [3-Feb-2007] $)
  $}

  $( This was the original version of ~ ax-11 , before it was discovered (in
     Jan. 2007) that the shorter ~ ax-11 could replace it.  It appears as Axiom
     scheme C15' in [Megill] p. 448 (p. 16 of the preprint).  As theorem
     ~ ax11o shows, it can be derived from ~ ax-11 and the other axioms.  An
     open problem is whether it can be derived from ~ ax-11 and the others when
     ~ ax-16 and ~ ax-17 are omitted .  See ~ ax-11 , ~ ax11o , and ~ ax11 for
     additional remarks.  If we wish to identify where this axiom is needed in
     the absence of ~ ax-16 and ~ ax-17 , we can use it in place of all
     references to theorem ~ ax11o in the theorems that follow. $)
  ax-11o $a |- ( -. A. x x = y ->
             ( x = y -> ( ph -> A. x ( x = y -> ph ) ) ) ) $.

  $( Rederivation of axiom ~ ax-11 from the orginal version, ~ ax-11o . $)
  ax11 $p |- ( x = y -> ( A. y ph -> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wi pm4.2i dral1 ax-1 19.20i syl6bir a1d wn ax-11o ax-4 syl7
    pm2.61i ) BCDZBEZRACEZRAFZBEZFZFSUCRSTABEUBAABCSAGHAUABARIJKLSMRAUBTABCNACO
    PQ $.
    $( [22-Jan-2007] $) $( [22-Jan-2007] $)

