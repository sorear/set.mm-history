$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
     Theorems without d.v. restrictions that rely on theorem ax11o
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A bidirectional version of ~ ax11o . $)
  ax11b $p |- ( ( -. A. x x = y /\ x = y ) ->
              ( ph <-> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wn wa wi ax11o imp ax-4 com12 adantl impbid ) BCDZBEFZOGAOAHZBEZP
    OARHABCIJORAHPROAQBKLMN $.
    $( [7-Jul-2006] $) $( [30-Jun-2006] $)

  $( Lemma used in proofs of substitution properties. $)
  equs5 $p |- ( -. A. x x = y ->
             ( E. x ( x = y /\ ph ) -> A. x ( x = y -> ph ) ) ) $=
    ( cv wceq wal wn wa wi hbnae hba1 ax11o imp3a 19.23ad ) BDCDEZBFGZOAHOAIZBF
    ZBBCBJQBKPOARABCLMN $.
    $( [5-Aug-1993] $)

  $( One direction of a simplified definition of substitution when variables
     are distinct. $)
  sb3 $p |- ( -. A. x x = y -> ( E. x ( x = y /\ ph ) -> [ y / x ] ph ) ) $=
    ( weq wal wn wa wex wi wsb equs5 sb2 syl6 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
    $.
    $( [5-Aug-1993] $)

  $( One direction of a simplified definition of substitution when variables
     are distinct. $)
  sb4 $p |- ( -. A. x x = y -> ( [ y / x ] ph -> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wn wa wex wi wsb equs5 sb1 syl5 ) BCDZBEFNAGBHNAIBEABCJABCKABCLM
    $.
    $( [5-Aug-1993] $)

  $( Simplified definition of substitution when variables are distinct. $)
  sb4b $p |- ( -. A. x x = y -> ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) ) $=
    ( weq wal wn wsb wi sb4 sb2 impbid1 ) BCDZBEFABCGLAHBEABCIABCJK $.
    $( [27-May-1997] $)

  $( An alternate definition of proper substitution that, like ~ df-sb , mixes
     free and bound variables to avoid distinct variable requirements. $)
  dfsb2 $p |- ( [ y / x ] ph <->
              ( ( x = y /\ ph ) \/ A. x ( x = y -> ph ) ) ) $=
    ( wsb weq wa wi wal wo sbequ2 a4s ax-4 jctild orc syl6 wn sb4 olc pm2.61i
    sbequ1 imp sb2 jaoi impbi ) ABCDZBCEZAFZUFAGBHZIZUFBHZUEUIGUJUEUGUIUJUEAUFU
    FUEAGBABCJKUFBLMUGUHNOUJPUEUHUIABCQUHUGROSUGUEUHUFAUEABCTUAABCUBUCUD $.
    $( [18-Feb-2005] $) $( [17-Feb-2005] $)

  $( An alternate definition of proper substitution ~ df-sb that uses only
     primitive connectives (no defined terms) on the right-hand side. $)
  dfsb3 $p |- ( [ y / x ] ph <->
              ( ( x = y -> -. ph ) -> A. x ( x = y -> ph ) ) ) $=
    ( weq wa wi wal wo wn wsb df-or dfsb2 imnan imbi1i 3bitr4 ) BCDZAEZPAFBGZHQ
    IZRFABCJPAIFZRFQRKABCLTSRPAMNO $.
    $( [18-Mar-2007] $) $( [6-Mar-2007] $)

  ${
    sb6y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb6y $p |- ( [ y / x ] ph <-> A. x ( x = y -> ph ) ) $=
      ( wsb weq wi wal sbequ2 a4s ax-10 alequcoms ax-1 19.20i syl6 syl5 syld
      sb4 pm2.61i sb2 impbi ) ABCEZBCFZAGZBHZUCBHZUBUEGUFUBAUEUCUBAGBABCIJUFACH
      ZUEAUFUGABHZUEUGUHGCBACBKLAUDBAUCMNODPQABCRSABCTUA $.
      $( [5-Aug-1993] $)
  $}

  $( Bound-variable hypothesis builder for substitution. $)
  hbsb2 $p |- ( -. A. x x = y -> ( [ y / x ] ph -> A. x [ y / x ] ph ) ) $=
    ( weq wal wn wsb wi sb4 sb2 a5i syl6 ) BCDZBEFABCGZMAHZBENBEABCIONBABCJKL
    $.
    $( [5-Aug-1993] $)

  $( An equality theorem for substitution. $)
  sbequi $p |- ( x = y -> ( [ x / z ] ph -> [ y / z ] ph ) ) $=
    ( weq wal wsb wi wn wa wex hbsb2 equvini stdpc7 sbequ1 sylan9 19.22i syl
    19.35 sylib hbnae 19.9d syl9 ex com23 sbequ2 a4s adantr drsb1 biimpd
    alequcoms sylan9r syld pm2.61ii ) DBEZDFZDCEZDFZBCEZADBGZADCGZHZHUPIZUSURIZ
    VBVCUSVDVBHVCUSJUTVADKZVDVAVCUTUTDFZUSVEADBLUSVBDKZVFVEHUSBDEZUQJZDKVGBCDMV
    IVBDVHUTAUQVAABDNADCOZPQRUTVADSTPVAVDDDCDUAADCLUBUCUDUEUPUSVBUPUSJUTAVAUPUT
    AHZUSUOVKDADBUFUGUHUSAABCGZUPVAABCOVLVAHBDVHBFVLVAABDCUIUJUKULUMUDURUSVBURU
    SJUTAVAURUTACBGZUSAURUTVMADCBUIUJABCNPURAVAHZUSUQVNDVJUGUHUMUDUN $.
    $( [5-Aug-1993] $)

  $( An equality theorem for substitution.  Used in proof of Theorem 9.7
     in [Megill] p. 449 (p. 16 of the preprint). $)
  sbequ $p |- ( x = y -> ( [ x / z ] ph <-> [ y / z ] ph ) ) $=
    ( weq wsb sbequi wi equcoms impbid ) BCEADBFZADCFZABCDGLKHCBACBDGIJ $.
    $( [5-Aug-1993] $)

  $( Formula-building lemma for use with the Distinctor Reduction Theorem.
     Part of Theorem 9.4 of [Megill] p. 448 (p. 16 of preprint). $)
  drsb2 $p |- ( A. x x = y -> ( [ x / z ] ph <-> [ y / z ] ph ) ) $=
    ( weq wal wsb wi sbequi a4s equcoms impbid ) BCEZBFADBGZADCGZMNOHBABCDIJMON
    HZBPCBACBDIKJL $.
    $( [5-Apr-2005] $) $( [27-Feb-2005] $)

  $( Removal of negation from substitution. $)
  sbn1 $p |- ( [ y / x ] -. ph -> -. [ y / x ] ph ) $=
    ( weq wal wn wsb wi sbequ2 nsyld a4s sb4 wa wex sb1 equs3 sylib con2i syl6
    pm2.61i ) BCDZBEZAFZBCGZABCGZFZHZUAUGBUAUDAUEUCBCIABCIJKUBFUDUAUCHBEZUFUCBC
    LUEUHUEUAAMBNUHFABCOABCPQRST $.
    $( [7-Aug-1994] $)

  $( Negation inside and outside of substitution are equivalent. $)
  sbn $p |- ( [ y / x ] -. ph <-> -. [ y / x ] ph ) $=
    ( wn wsb sbn1 sbn2 impbi ) ADBCEABCEDABCFABCGH $.
    $( [5-Aug-1993] $)

  ${
    sb5y.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalence involving substitution with a variable not free. $)
    sb5y $p |- ( [ y / x ] ph <-> E. x ( x = y /\ ph ) ) $=
      ( wsb weq wn wi wal wa wex hbn sb6y sbn bitr3 con2bii equs3 bitr4 ) ABCEZ
      BCFZAGZHBIZGTAJBKUBSUBUABCESGUABCACDLMABCNOPABCQR $.
      $( [5-Aug-1993] $)
  $}

  $( Removal of implication from substitution. $)
  sbi1 $p |- ( [ y / x ] ( ph -> ps ) -> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( weq wal wi wsb sbequ2 syl5d sbequ1 syl6d a4s wn sb4 ax-2 19.20ii sb2
    syl6 pm2.61i ) CDEZCFZABGZCDHZACDHZBCDHZGGZUAUGCUAUDUEBUFUAUDABUEUCCDIACDIJ
    BCDKLMUBNZUDUAAGZCFZUFUEUHUDUAUCGZCFZUJUFGUCCDOULUJUABGZCFUFUKUIUMCUAABPQBC
    DRSSACDOJT $.
    $( [5-Aug-1993] $)

  $( Introduction of implication into substitution. $)
  sbi2 $p |- ( ( [ y / x ] ph -> [ y / x ] ps ) -> [ y / x ] ( ph -> ps ) ) $=
    ( wsb wi wn sbn pm2.21 sbimi sylbir ax-1 ja ) ACDEZBCDEABFZCDEZNGAGZCDEPACD
    HQOCDABIJKBOCDBALJM $.
    $( [5-Aug-1993] $)

  $( Implication inside and outside of substitution are equivalent. $)
  sbim $p |- ( [ y / x ] ( ph -> ps ) <-> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( wi wsb sbi1 sbi2 impbi ) ABECDFACDFBCDFEABCDGABCDHI $.
    $( [5-Aug-1993] $)

  $( Logical OR inside and outside of substitution are equivalent. $)
  sbor $p |- ( [ y / x ] ( ph \/ ps ) <-> ( [ y / x ] ph \/ [ y / x ] ps ) ) $=
    ( wn wi wsb wo sbim sbn imbi1i bitr df-or sbbii 3bitr4 ) AEZBFZCDGZACDGZEZB
    CDGZFZABHZCDGSUAHRPCDGZUAFUBPBCDIUDTUAACDJKLUCQCDABMNSUAMO $.
    $( [29-Sep-2002] $)

  ${
    sb19.21.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution with a variable not free in antecedent affects only the
       consequent. $)
    sb19.21 $p |- ( [ y / x ] ( ph -> ps ) <-> ( ph -> [ y / x ] ps ) ) $=
      ( wi wsb sbim sbf imbi1i bitr ) ABFCDGACDGZBCDGZFAMFABCDHLAMACDEIJK $.
      $( [5-Aug-1993] $)
  $}

  $( Conjunction inside and outside of a substitution are equivalent. $)
  sban $p |- ( [ y / x ] ( ph /\ ps ) <-> ( [ y / x ] ph /\ [ y / x ] ps ) ) $=
    ( wn wi wsb wa sbn sbim imbi2i bitr negbii df-an sbbii 3bitr4 ) ABEZFZEZCDG
    ZACDGZBCDGZEZFZEZABHZCDGUAUBHTRCDGZEUERCDIUGUDUGUAQCDGZFUDAQCDJUHUCUABCDIKL
    MLUFSCDABNOUAUBNP $.
    $( [5-Aug-1993] $)

  $( Conjunction inside and outside of a substitution are equivalent. $)
  sb3an $p |- ( [ y / x ] ( ph /\ ps /\ ch ) <->
              ( [ y / x ] ph /\ [ y / x ] ps /\ [ y / x ] ch ) ) $=
    ( w3a wsb wa df-3an sbbii sban anbi1i bitr4 3bitr ) ABCFZDEGABHZCHZDEGPDEGZ
    CDEGZHZADEGZBDEGZSFZOQDEABCIJPCDEKTUAUBHZSHUCRUDSABDEKLUAUBSIMN $.
    $( [16-Dec-2006] $) $( [14-Dec-2006] $)

  $( Equivalence inside and outside of a substitution are equivalent. $)
  sbbi $p |- ( [ y / x ] ( ph <-> ps ) <-> ( [ y / x ] ph <-> [ y / x ] ps ) )
             $=
    ( wb wsb wi wa bi sbbii sbim anbi12i sban 3bitr4 bitr ) ABEZCDFABGZBAGZHZCD
    FZACDFZBCDFZEZPSCDABIJQCDFZRCDFZHUAUBGZUBUAGZHTUCUDUFUEUGABCDKBACDKLQRCDMUA
    UBINO $.
    $( [5-Aug-1993] $)

  ${
    sblbis.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce left biconditional inside of a substitution. $)
    sblbis $p |- ( [ y / x ] ( ch <-> ph ) <-> ( [ y / x ] ch <-> ps ) ) $=
      ( wb wsb sbbi bibi2i bitr ) CAGDEHCDEHZADEHZGLBGCADEIMBLFJK $.
      $( [19-Aug-1993] $)
  $}

  ${
    sbrbis.1 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbis $p |- ( [ y / x ] ( ph <-> ch ) <-> ( ps <-> [ y / x ] ch ) ) $=
      ( wb wsb sbbi bibi1i bitr ) ACGDEHADEHZCDEHZGBMGACDEILBMFJK $.
      $( [18-Aug-1993] $)
  $}

  ${
    sbrbif.1 $e |- ( ch -> A. x ch ) $.
    sbrbif.2 $e |- ( [ y / x ] ph <-> ps ) $.
    $( Introduce right biconditional inside of a substitution. $)
    sbrbif $p |- ( [ y / x ] ( ph <-> ch ) <-> ( ps <-> ch ) ) $=
      ( wb wsb sbrbis sbf bibi2i bitr ) ACHDEIBCDEIZHBCHABCDEGJNCBCDEFKLM $.
      $( [18-Aug-1993] $)
  $}

  $( A specialization theorem. $)
  sbea4 $p |- ( [ y / x ] ph -> E. x ph ) $=
    ( wsb wn wal wex stdpc4 sbn sylib con2i df-ex sylibr ) ABCDZAEZBFZEABGPNPOB
    CDNEOBCHABCIJKABLM $.
    $( [5-Aug-1993] $)

  $( Specialization of implication. $)
  sbia4 $p |- ( A. x ( ph -> ps ) -> ( [ y / x ] ph -> [ y / x ] ps ) ) $=
    ( wi wal wsb stdpc4 sbim sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.
    $( [5-Aug-1993] $)

  $( Specialization of biconditional. $)
  sbba4 $p |- ( A. x ( ph <-> ps ) -> ( [ y / x ] ph <-> [ y / x ] ps ) ) $=
    ( wb wal wsb stdpc4 sbbi sylib ) ABEZCFKCDGACDGBCDGEKCDHABCDIJ $.
    $( [5-Aug-1993] $)

  ${
    sbbid.1 $e |- ( ph -> A. x ph ) $.
    sbbid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Deduction substituting both sides of a biconditional. $)
    sbbid $p |- ( ph -> ( [ y / x ] ps <-> [ y / x ] ch ) ) $=
      ( wb wal wsb 19.21ai sbba4 syl ) ABCHZDIBDEJCDEJHANDFGKBCDELM $.
      $( [5-Aug-1993] $)
  $}

  $( Elimination of equality from antecedent after substitution. $)
  sbequ8 $p |- ( [ y / x ] ph <-> [ y / x ] ( x = y -> ph ) ) $=
    ( wsb weq wi equsb1 a1bi sbim bitr4 ) ABCDZBCEZBCDZKFLAFBCDMKBCGHLABCIJ $.
    $( [5-Aug-1993] $)

  ${
    hbsb4.1 $e |- ( ph -> A. z ph ) $.
    $( A variable not free remains so after substitution with a distinct
       variable. $)
    hbsb4 $p |- ( -. A. z z = y -> ( [ y / x ] ph -> A. z [ y / x ] ph ) ) $=
      ( weq wal wn wsb wi wb equequ1 a4s dral1 negbid hbsb2 ax-10 alequcoms
      syl9r sylbid wa hbae ax-4 19.20i sbequ2 sbequ1 19.20ii syl5 syld 3syl
      a1d sb4 hbnae hban ax-12 imp a1i hbimd 19.20d sb2 a7s syl6 syl9 pm2.61i
      ex ) DBFZDGZDCFZDGZHZABCIZVKDGZJZJVGVJBCFZBGZHZVMVGVIVOVHVNDBVFVHVNKDDBCL
      MNOVPVKVKBGZVGVLABCPVQVLJBDVKBDQRSTVGHZVJVMVOVRVJUAZVMJVOVMVSVOVODGVNDGZV
      MBCDUBVOVNDVNBUCUDVTVKAVLVNVKAJDABCUEMVTADGZVLAVNAVKDABCUFUGEUHUIUJUKVPVK
      VNAJZBGZVSVLABCULVSWCWBDGZBGVLVSWBWDBVRVJBDBBUMDCBUMUNVSVNADVRVJDDBDUMDCD
      UMUNVRVJVNVTJBCDUOUPAWAJVSEUQURUSWBVLDBWCVKDABCUTUDVAVBVCVDVEVD $.
      $( [5-Aug-1993] $)
  $}

  $( A variable not free remains so after substitution with a distinct
     variable (closed form of ~ hbsb4 ). $)
  hbsb4t $p |- ( A. x A. z ( ph -> A. z ph ) ->
               ( -. A. z z = y -> ( [ y / x ] ph -> A. z [ y / x ] ph ) ) ) $=
    ( wal wi wsb weq wn wb wa ax-4 biantru bi bitr4 2albii sbba4 a4s hba1
    albid imbi12d a7s sylbi hbsb4 syl5bir ) AADEZFZDEBEZABCGZUIDEZFZUFBCGZULDEZ
    FZDCHDEIUHAUFJZDEBEUKUNJZUGUOBDUGUGUFAFZKUOUQUGADLMAUFNOPUOUPDBUOBEZDEZUIUL
    UJUMURUIULJDAUFBCQRZUSUIULDURDSUTTUAUBUCUFBCDADSUDUE $.
    $( [14-Apr-2004] $) $( [7-Apr-2004] $)

  ${
    dvelimf.1 $e |- ( ph -> A. x ph ) $.
    dvelimf.2 $e |- ( ps -> A. z ps ) $.
    dvelimf.3 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( Version of ~ dvelim without any variable restrictions. $)
    dvelimf $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( weq wal wn wsb hbsb4 sbie albii 3imtr3g ) CDICJKAEDLZQCJBBCJAEDCFMABEDG
      HNZQBCROP $.
      $( [1-Oct-2002] $)
  $}

  ${
    dvelimdf.1 $e |- ( ph -> A. x ph ) $.
    dvelimdf.2 $e |- ( ph -> A. z ph ) $.
    dvelimdf.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    dvelimdf.4 $e |- ( ph -> ( ch -> A. z ch ) ) $.
    dvelimdf.5 $e |- ( ph -> ( z = y -> ( ps <-> ch ) ) ) $.
    $( Deduction form of ~ dvelimf .  This version may be useful if we want to
       avoid ~ ax-17 and use ~ ax-16 instead. $)
    dvelimdf $p |- ( ph -> ( -. A. x x = y -> ( ch -> A. x ch ) ) ) $=
      ( weq wal wn wi wa wsb 19.21ai 19.20i2 hbsb4t 3syl imp wb sbied adantr
      albid 3imtr3d ex ) ADELDMNZCCDMZOAUIPBFEQZUKDMZCUJAUIUKULOZAADMZFMBBDMOZD
      MFMUIUMOAUNFHGRAUOFDISBFEDTUAUBAUKCUCUIABCFEHJKUDZUEAULUJUCUIAUKCDGUPUFUE
      UGUH $.
      $( [15-Apr-2004] $) $( [7-Apr-2004] $)
  $}

  $( A composition law for substitution. $)
  sbco $p |- ( [ y / x ] [ x / y ] ph <-> [ y / x ] ph ) $=
    ( wsb wb weq equsb2 sbequ12 bicomd sbimi ax-mp sbbi mpbi ) ACBDZAEZBCDZNBCD
    ABCDECBFZBCDPBCGQOBCQANACBHIJKNABCLM $.
    $( [5-Aug-1993] $)

  ${
    sbid2.1 $e |- ( ph -> A. x ph ) $.
    $( An identity law for substitution. $)
    sbid2 $p |- ( [ y / x ] [ x / y ] ph <-> ph ) $=
      ( wsb sbco sbf bitr ) ACBEBCEABCEAABCFABCDGH $.
      $( [5-Aug-1993] $)
  $}

  $( An idempotent law for substitution. $)
  sbidm $p |- ( [ y / x ] [ y / x ] ph <-> [ y / x ] ph ) $=
    ( weq wal wsb wb sbequ12 bicomd a4s wn hbnae hbsb2 wi pm4.2i a1i sbied
    pm2.61i ) BCDZBEZABCFZBCFZUAGZSUCBSUAUBUABCHIJTKZUAUABCBCBLABCMSUAUAGNUDSUA
    OPQR $.
    $( [30-Jun-1994] $)

  ${
    sbco2.1 $e |- ( ph -> A. z ph ) $.
    $( A composition law for substitution. $)
    sbco2 $p |- ( [ y / z ] [ z / x ] ph <-> [ y / x ] ph ) $=
      ( weq wal wsb wb sbequ sbid2 syl5bbr sbequ12 bitr3d a4s wn hbnae hbsb3
      hbsb4 wi a1i sbied bicomd pm2.61i ) BCFZBGZABDHZDCHZABCHZIZUEUJBUEAUHUIUE
      UGDBHUHAUGBCDJADBEKLZABCMNOUFPZUIUHULAUHBCBCBQUGDCBABDERSUEAUHITULUKUAUBU
      CUD $.
      $( [30-Jun-1994] $)
  $}

  ${
    sbco2d.1 $e |- ( ph -> A. x ph ) $.
    sbco2d.2 $e |- ( ph -> A. z ph ) $.
    sbco2d.3 $e |- ( ph -> ( ps -> A. z ps ) ) $.
    $( A composition law for substitution. $)
    sbco2d $p |- ( ph -> ( [ y / z ] [ z / x ] ps <-> [ y / x ] ps ) ) $=
      ( wsb wi hbim1 sbco2 sb19.21 sbbii bitr 3bitr3 pm5.74ri ) ABCEIZEDIZBCDIZ
      ABJZCEIZEDIZUACDIASJZATJUACDEABEGHKLUCARJZEDIUDUBUEEDABCEFMNAREDGMOABCDFM
      PQ $.
      $( [5-Aug-1993] $)
  $}

  $( A composition law for substitution. $)
  sbco3 $p |- ( [ z / y ] [ y / x ] ph <-> [ z / x ] [ x / y ] ph ) $=
    ( weq wal wsb wb drsb1 sbequ12a 19.20i sbba4 syl bitr3d wn hbnae hbsb2
    sbco2d sbco sbbii syl5rbbr pm2.61i ) BCEZBFZABCGZCDGZACBGZBDGZHUDUEBDGZUFUH
    UEBCDIUDUEUGHZBFUIUHHUCUJBABCJKUEUGBDLMNUDOZUECBGZBDGUFUHUKUECDBBCCPBCBPABC
    QRULUGBDACBSTUAUB $.
    $( [5-Aug-1993] $)

  $( A commutativity law for substitution. $)
  sbcom $p |- ( [ y / z ] [ y / x ] ph <-> [ y / x ] [ y / z ] ph ) $=
    ( weq wal wsb wb wn wa drsb1 hbae sbbid bitr3d adantr wi hbnae hban ax-12
    imp 19.20i 19.21t 3syl albid adantrr nalequcoms bi2.04 albii syl5bb alcom
    adantrl sb4b imbi2d sylan9bbr adantl sylan9bb 3bitr4d pm2.61ian ex sbequ12
    a4s pm2.61ii ) BCEZBFZDCEZDFZABCGZDCGZADCGZBCGZHZVDIZVFIZVKBDEBFZVLVMJZVKVN
    VKVOVNVGBCGVHVJVGBDCKVNVGVIBCBDBLABDCKMNOVNIZVOJZVEVCAPZBFZPZDFZVCVEAPZDFZP
    ZBFZVHVJVQVEVRPZBFZDFZWAWEVPVLWHWAHVMVPVLJZWGVTDVPVLDBDDQZBCDQZRWIWIBFVEVEB
    FPZBFWGVTHVPVLBBDBQZBCBQRWIWLBVPVLWLDCBSTUAVEVRBUBUCUDUEVPVMWHWEHVLVPVMJZWF
    DFZBFWEWHWNWOWDBVPVMBWMDCBQZRWNVCWBPZDFZWDWOWNWNDFVCVCDFPZDFWRWDHVPVMDWJDCD
    QRWNWSDVPVMWSVMWSPDBBCDSUFTUAVCWBDUBUCWFWQDVEVCAUGUHUIUDWFDBUJUIUKNVOVHWAHV
    PVMVHVEVGPZDFVLWAVGDCULVLWTVTDWKVLVGVSVEABCULUMUDUNUOVOVJWEHVPVLVJVCVIPZBFV
    MWEVIBCULVMXAWDBWPVMVIWCVCADCULUMUDUPUOUQURUSVDVIVHVJVDAVGDCBCDLVCAVGHBABCU
    TVAMVCVIVJHBVIBCUTVANVFVGVHVJVEVGVHHDVGDCUTVAVFAVIBCDCBLVEAVIHDADCUTVAMNVB
    $.
    $( [27-May-1997] $)

  ${
    sb5rf.1 $e |- ( ph -> A. y ph ) $.
    $( Reversed substitution. $)
    sb5rf $p |- ( ph <-> E. y ( y = x /\ [ y / x ] ph ) ) $=
      ( weq wsb wa wex sbid2 sb1 sylbir sbequ12r biimpa 19.23ai impbi ) ACBEZAB
      CFZGZCHZAQCBFSACBDIQCBJKRACDPQAACBLMNO $.
      $( [22-Mar-2005] $) $( [3-Feb-2005] $)

    $( Reversed substitution. $)
    sb6rf $p |- ( ph <-> A. y ( y = x -> [ y / x ] ph ) ) $=
      ( weq wsb wi wal sbequ1 equcoms com12 19.21ai sb2 sbco sylib sbf impbi )
      ACBEZABCFZGZCHZATCDRASASGBCABCIJKLUAACBFZAUASCBFUBSCBMACBNOACBDPOQ $.
      $( [5-Aug-1993] $)
  $}

  ${
    sb8.1 $e |- ( ph -> A. y ph ) $.
    $( Substitution of variable in universal quantifier. $)
    sb8 $p |- ( A. x ph <-> A. y [ y / x ] ph ) $=
      ( wal wsb hbal stdpc4 19.21ai hbsb3 sbid2 sylib impbi ) ABEZABCFZCEZNOCAC
      BDGABCHIPABOBCABCDJGPOCBFAOCBHACBDKLIM $.
      $( [5-Aug-1993] $)
  $}

  ${
    sb8e.1 $e |- ( ph -> A. y ph ) $.
    $( Substitution of variable in existential quantifier. $)
    sb8e $p |- ( E. x ph <-> E. y [ y / x ] ph ) $=
      ( wn wal wsb wex hbn sb8 sbn albii bitr negbii df-ex 3bitr4 ) AEZBFZEABCG
      ZEZCFZEABHSCHRUARQBCGZCFUAQBCACDIJUBTCABCKLMNABOSCOP $.
      $( [12-Aug-1993] $)
  $}

  $( Commutation of quantification and substitution variables. $)
  sb9i $p |- ( A. x [ x / y ] ph -> A. y [ y / x ] ph ) $=
    ( weq wal wsb wi drsb1 drsb2 bitr3d dral1 biimprd wn hbsb2 19.20ii hbnaes
    stdpc4 sbco sylib 19.20i a7s syl6 pm2.61i ) CBDCEZACBFZBEZABCFZCEZGUDUHUFUG
    UECBUDACCFUGUEACBCHACBCIJKLUDMZUFUECEZBEZUHUFUKGCBBUIUEUJBACBNOPUEUHCBUFUGC
    UFUEBCFUGUEBCQABCRSTUAUBUC $.
    $( [5-Aug-1993] $)

  $( Commutation of quantification and substitution variables. $)
  sb9 $p |- ( A. x [ x / y ] ph <-> A. y [ y / x ] ph ) $=
    ( wsb wal sb9i impbi ) ACBDBEABCDCEABCFACBFG $.
    $( [5-Aug-1993] $)

