$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Existential uniqueness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed for uniqueness notation. $)
  $c E! $.  $( Backwards E exclamation point. $)
  $c E* $.  $( Backwards E superscript *. $)

  $( Extend wff definition to include existential uniqueness ("there exists a
     unique ` x ` such that ` ph ` "). $)
  weu $a wff E! x ph $.

  $( Extend wff definition to include uniqueness ("there exists at most one
     ` x ` such that ` ph ` "). $)
  wmo $a wff E* x ph $.

  ${
    $d x y $.  $d y ph $.
    $( Define existential uniqueness, i.e. "there exists exactly one ` x `
       such that ` ph ` ."  Definition 10.1 of [BellMachover] p. 97; also
       Definition *14.02 of [WhiteheadRussell] p. 175.  Other possible
       definitions are given by ~ eu1 , ~ eu2 , ~ eu3 , and ~ eu5 (which in
       some cases we show with a hypothesis ` ph -> A. y ph ` in place of a
       distinct variable condition on ` y ` and ` ph ` ).  Double uniqueness is
       tricky:  ` E! x E! y ph ` does not mean "exactly one ` x ` and one
       ` y ` " (see ~ 2eu4 ). $)
    df-eu $a |- ( E! x ph <-> E. y A. x ( ph <-> x = y ) ) $.
  $}

  $( Define "there exists at most one ` x ` such that ` ph ` ". Here we
     define it in terms of existential uniqueness.  Notation of [BellMachover]
     p. 460, whose definition we show as ~ mo3 . For other possible
     definitions see ~ mo2 and ~ mo4 . $)
  df-mo $a |- ( E* x ph <-> ( E. x ph -> E! x ph ) ) $.

  ${
    $d x y z $.  $d ph z $.
    euf.1 $e |- ( ph -> A. y ph ) $.
    $( A version of the existential uniqueness definition with a hypothesis
       instead of a distinct variable condition. $)
    euf $p |- ( E! x ph <-> E. y A. x ( ph <-> x = y ) ) $=
      ( vz weu weq wb wal wex df-eu ax-17 hbbi hbal equequ2 bibi2d albidv
      cbvex bitr ) ABFABEGZHZBIZEJABCGZHZBIZCJABEKUBUEECUACBATCDTCLMNUEELECGZUA
      UDBUFTUCAECBOPQRS $.
      $( [12-Aug-1993] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.  $d y ch $.
    eubid.1 $e |- ( ph -> A. x ph ) $.
    eubid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for uniqueness quantifier (deduction rule). $)
    eubid $p |- ( ph -> ( E! x ps <-> E! x ch ) ) $=
      ( vy weq wb wal wex weu bibi1d albid exbidv df-eu 3bitr4g ) ABDGHZIZDJZGK
      CRIZDJZGKBDLCDLATUBGASUADEABCRFMNOBDGPCDGPQ $.
      $( [9-Jul-1994] $)
  $}

  ${
    $d x ph $.
    eubidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for uniqueness quantifier (deduction rule). $)
    eubidv $p |- ( ph -> ( E! x ps <-> E! x ch ) ) $=
      ( ax-17 eubid ) ABCDADFEG $.
      $( [9-Jul-1994] $)
  $}

  ${
    eubii.1 $e |- ( ph <-> ps ) $.
    $( Introduce uniqueness quantifier to both sides of an equivalence. $)
    eubii $p |- ( E! x ph <-> E! x ps ) $=
      ( weq weu wb equid hbequid a1i eubid ax-mp ) CCEZACFBCFGCHMABCCIABGMDJKL
      $.
      $( [9-Jul-1994] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( Bound-variable hypothesis builder for uniqueness. $)
    hbeu1 $p |- ( E! x ph -> A. x E! x ph ) $=
      ( vy weq wb wal wex weu hba1 hbex df-eu albii 3imtr4 ) ABCDEZBFZCGZPBFABH
      ZQBFOBCNBIJABCKZQPBRLM $.
      $( [9-Jul-1994] $)
  $}

  ${
    $d y z $.  $d x z $.  $d z ph $.
    hbeu.1 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for "at most one".  Note that ` x `
       and ` y ` needn't be distinct (this makes the proof more difficult). $)
    hbeu $p |- ( E! y ph -> A. x E! y ph ) $=
      ( vz weq wb wal wex weu wi ax-10 alequcoms hba1 syl5 wn hbnae a1i dveeq1
      hbbid hbald pm2.61i hbex df-eu albii 3imtr4 ) ACEFZGZCHZEIZUJBHACJZUKBHUI
      BEBCFBHZUIUIBHZKULUICHZUMUIUNUMKCBUICBLMUHCNOULPZUHBCBCCQUOAUGBBCBQAABHKU
      ODRBCESTUAUBUCACEUDZUKUJBUPUEUF $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    sb8eu.1 $e |- ( ph -> A. y ph ) $.
    $( Variable substitution in uniqueness quantifier.  (This theorem can also
       be proved without requiring that ` x ` and ` y ` be distinct, but
       the proof would be longer.) $)
    sb8eu $p |- ( E! x ph <-> E! y [ y / x ] ph ) $=
      ( vz weq wb wal wex wsb weu ax-17 hbbi sb8 equequ1 sbie sblbis albii
      bitr exbii df-eu 3bitr4 ) ABEFZGZBHZEIABCJZCEFZGZCHZEIABKUFCKUEUIEUEUDBCJ
      ZCHUIUDBCAUCCDUCCLMNUJUHCUCUGABCUCUGBCUGBLBCEOPQRSTABEUAUFCEUAUB $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x y $.
    cbveu.1 $e |- ( ph -> A. y ph ) $.
    cbveu.2 $e |- ( ps -> A. x ps ) $.
    cbveu.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbveu $p |- ( E! x ph <-> E! y ps ) $=
      ( weu wsb sb8eu sbie eubii bitr ) ACHACDIZDHBDHACDEJNBDABCDFGKLM $.
      $( [25-Nov-1994] $)
  $}

  ${
    $d x y $.
    eu1.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of expressing uniqueness used by some authors.
       Exercise 2(b) of [Margaris] p. 110. $)
    eu1 $p |- ( E! x ph <->
                E. x ( ph /\ A. y ( [ y / x ] ph -> x = y ) ) ) $=
      ( wsb weu weq wb wal wex wi wa hbs1 euf sb8eu equcom imbi2i albii sb6rf
      anbi12i ancom albi 3bitr4 exbii ) ABCEZCFUECBGZHCIZBJABFAUEBCGZKZCIZLZBJU
      ECBABCMNABCDOUKUGBUJALUEUFKZCIZUFUEKCIZLUKUGUJUMAUNUIULCUHUFUEBCPQRABCDST
      AUJUAUEUFCUBUCUDUC $.
      $( [20-Aug-1993] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    mo.1 $e |- ( ph -> A. y ph ) $.
    $( Equivalent definitions of "there exists at most one". $)
    mo $p |- ( E. y A. x ( ph -> x = y ) <->
               A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) ) $=
      ( vz weq wi wal wex wsb wa ax-17 hbim hbal equequ2 imbi2d albidv cbvex
      hbs1 sbequ2 ax-8 imim12d cbv3 ancli aaan sylibr prth equtr2 syl6 19.20i2
      syl 19.23aiv sylbir 19.20 19.20i a7s 19.22 hbsb3 19.22i syl5com impexp
      bi2.04 bitr 2albii syl5ib wn alnex hbne sbequ1 equcoms con3d pm2.21
      19.8a 3syl a1d pm2.61i impbi ) ABCFZGZBHZCIZAABCJZKZVRGZCHBHZWAABEFZGZBHZ
      EIWEWHVTECWGCBAWFCDWFCLMZNVTELECFZWGVSBWJWFVRAECBOPQRWHWEEWHWGWBCEFZGZKZC
      HBHZWEWHWHWLCHZKWNWHWOWGWLBCWIWBWKBABCSWKBLMZVRWBAWFWKABCTBCEUAUBUCUDWGWL
      BCWIWPUEUFWMWDBCWMWCWFWKKVRAWFWBWKUGBCEUHUIUJUKULUMWBCIZWEWAGWQWBVSGZCHBH
      ZWAWEWSWBBHZCIZWAWQWSWTVTGZCHZXAWAGWRXCCBWRBHXBCWBVSBUNUOUPWTVTCUQUKWBWTC
      ABCDURZUSUTWDWRBCWDAWBVRGGWRAWBVRVAAWBVRVBVCVDVEWQVFZWAWEXEWBVFZCHZWAWBCV
      GXGAVFZBHVTWAXFXHCBWBBXDVHACDVHCBFAWBAWBGBCABCVIVJVKUCXHVSBAVRVLUOVTCVMVN
      UMVOVPVQ $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Existential uniqueness implies existence. $)
    euex $p |- ( E! x ph -> E. x ph ) $=
      ( vy weu wex wsb weq wi wal wa ax-17 eu1 19.40 sylbi pm3.26d ) ABDZABEZAB
      CFBCGHCIZBEZPARJBEQSJABCACKLARBMNO $.
      $( [15-Sep-1993] $)
  $}

  ${
    $d x y $.
    eumo0.1 $e |- ( ph -> A. y ph ) $.
    $( Existential uniqueness implies "at most one". $)
    eumo0 $p |- ( E! x ph -> E. y A. x ( ph -> x = y ) ) $=
      ( weu weq wb wal wex wi euf bi1 19.20i 19.22i sylbi ) ABEABCFZGZBHZCIAPJZ
      BHZCIABCDKRTCQSBAPLMNO $.
      $( [8-Jul-1994] $)
  $}

  ${
    $d x y $.
    eu2.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of defining existential uniqueness.  Definition 6.10 of
       [TakeutiZaring] p. 26. $)
    eu2 $p |- ( E! x ph <->
             ( E. x ph /\ A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) ) )
             $=
      ( weu wex wsb wa weq wi wal euex eumo0 mo sylib jca 19.29r impexp albii
      19.21 bitr anbi2i abai bitr4 exbii eu1 sylibr impbi ) ABEZABFZAABCGZHBCIZ
      JZCKZBKZHZUIUJUOABLUIAULJBKCFUOABCDMABCDNOPUPAUKULJZCKZHZBFZUIUPAUNHZBFUT
      AUNBQVAUSBVAAAURJZHUSUNVBAUNAUQJZCKVBUMVCCAUKULRSAUQCDTUAUBAURUCUDUEOABCD
      UFUGUH $.
      $( [8-Jul-1994] $)
  $}

  ${
    $d x y $.
    eu3.1 $e |- ( ph -> A. y ph ) $.
    $( An alternate way of expressing existential uniqueness. $)
    eu3 $p |- ( E! x ph <->
                ( E. x ph /\ E. y A. x ( ph -> x = y ) ) ) $=
      ( weu wex wsb wa weq wi wal eu2 mo anbi2i bitr4 ) ABEABFZAABCGHBCIZJCKBKZ
      HPAQJBKCFZHABCDLSRPABCDMNO $.
      $( [8-Jul-1994] $)
  $}

  ${
    euor.1 $e |- ( ph -> A. x ph ) $.
    $( Introduce a disjunct into a uniqueness quantifier. $)
    euor $p |- ( ( -. ph /\ E! x ps ) -> E! x ( ph \/ ps ) ) $=
      ( wn weu wo hbne biorf eubid biimpa ) AEZBCFABGZCFLBMCACDHABIJK $.
      $( [22-Oct-2005] $) $( [21-Oct-2005] $)
  $}

  ${
    $d x ph $.
    $( Introduce a disjunct into a uniqueness quantifier. $)
    euorv $p |- ( ( -. ph /\ E! x ps ) -> E! x ( ph \/ ps ) ) $=
      ( ax-17 euor ) ABCACDE $.
      $( [23-Mar-1995] $)
  $}

  ${
    $d x y $.
    mo2.1 $e |- ( ph -> A. y ph ) $.
    $( Alternate definition of "at most one". $)
    mo2 $p |- ( E* x ph <-> E. y A. x ( ph -> x = y ) ) $=
      ( wmo wex weu wi weq wal df-mo wn alnex pm2.21 19.20i 19.8a syl sylbir
      eumo0 ja wa eu3 biimpr expcom impbi bitr ) ABEABFZABGZHZABCIZHZBJZCFZABKU
      IUMUGUHUMUGLALZBJZUMABMUOULUMUNUKBAUJNOULCPQRABCDSTUGUMUHUHUGUMUAABCDUBUC
      UDUEUF $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y $.
    mo3.1 $e |- ( ph -> A. y ph ) $.
    $( Alternate definition of "at most one".  Definition of [BellMachover]
       p. 460, except that definition has the side condition that ` y ` not
       occur in ` ph ` in place of our hypothesis. $)
    mo3 $p |- ( E* x ph <->
               A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) ) $=
      ( wmo weq wi wal wex wsb wa mo2 mo bitr ) ABEABCFZGBHCIAABCJKOGCHBHABCDLA
      BCDMN $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y $.  $d y ph $.
    mo4f.1 $e |- ( ps -> A. x ps ) $.
    mo4f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( "At most one" expressed using implicit substitution. $)
    mo4f $p |- ( E* x ph <-> A. x A. y ( ( ph /\ ps ) -> x = y ) ) $=
      ( wmo wsb wa weq wi wal ax-17 mo3 sbie anbi2i imbi1i 2albii bitr ) ACGAAC
      DHZIZCDJZKZDLCLABIZUBKZDLCLACDADMNUCUECDUAUDUBTBAABCDEFOPQRS $.
      $( [12-Apr-2004] $) $( [10-Apr-2004] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    mo4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( "At most one" expressed using implicit substitution. $)
    mo4 $p |- ( E* x ph <-> A. x A. y ( ( ph /\ ps ) -> x = y ) ) $=
      ( ax-17 mo4f ) ABCDBCFEG $.
      $( [26-Jul-1995] $)
  $}

  ${
    mobid.1 $e |- ( ph -> A. x ph ) $.
    mobid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for "at most one" quantifier (deduction rule). $)
    mobid $p |- ( ph -> ( E* x ps <-> E* x ch ) ) $=
      ( wex weu wi wmo exbid eubid imbi12d df-mo 3bitr4g ) ABDGZBDHZICDGZCDHZIB
      DJCDJAPRQSABCDEFKABCDEFLMBDNCDNO $.
      $( [8-Mar-1995] $)
  $}

  ${
    mobii.1 $e |- ( ps <-> ch ) $.
    $( Formula-building rule for "at most one" quantifier (inference rule). $)
    mobii $p |- ( E* x ps <-> E* x ch ) $=
      ( weq wmo wb equid hbequid a1i mobid ax-mp ) CCEZACFBCFGCHMABCCIABGMDJKL
      $.
      $( [9-Mar-1995] $)
  $}

  $( Bound-variable hypothesis builder for "at most one". $)
  hbmo1 $p |- ( E* x ph -> A. x E* x ph ) $=
    ( wex weu wi wal wmo hbe1 hbeu1 hbim df-mo albii 3imtr4 ) ABCZABDZEZPBFABGZ
    QBFNOBABHABIJABKZQPBRLM $.
    $( [8-Mar-1995] $)

  ${
    hbmo.1 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for "at most one". $)
    hbmo $p |- ( E* y ph -> A. x E* y ph ) $=
      ( wex weu wi wal wmo hbex hbeu hbim df-mo albii 3imtr4 ) ACEZACFZGZRBHACI
      ZSBHPQBABCDJABCDKLACMZSRBTNO $.
      $( [9-Mar-1995] $)
  $}

  ${
    $d x y $.
    cbvmo.1 $e |- ( ph -> A. y ph ) $.
    cbvmo.2 $e |- ( ps -> A. x ps ) $.
    cbvmo.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvmo $p |- ( E* x ph <-> E* y ps ) $=
      ( wex weu wi wmo cbvex cbveu imbi12i df-mo 3bitr4 ) ACHZACIZJBDHZBDIZJACK
      BDKQSRTABCDEFGLABCDEFGMNACOBDOP $.
      $( [9-Mar-1995] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( Uniqueness in terms of "at most one". $)
    eu5 $p |- ( E! x ph <-> ( E. x ph /\ E* x ph ) ) $=
      ( vy weu wex weq wi wal wa wmo ax-17 eu3 mo2 anbi2i bitr4 ) ABDABEZABCFGB
      HCEZIPABJZIABCACKZLRQPABCSMNO $.
      $( [23-Mar-1995] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    eu4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Uniqueness using implicit substitution. $)
    eu4 $p |- ( E! x ph <-> ( E. x ph /\
             A. x A. y ( ( ph /\ ps ) -> x = y ) ) ) $=
      ( weu wex wmo wa weq wi wal eu5 mo4 anbi2i bitr ) ACFACGZACHZIQABICDJKDLC
      LZIACMRSQABCDENOP $.
      $( [26-Jul-1995] $)
  $}

  $( Existential uniqueness implies "at most one". $)
  eumo $p |- ( E! x ph -> E* x ph ) $=
    ( weu wex wmo eu5 pm3.27bd ) ABCABDABEABFG $.
    $( [23-Mar-1995] $)

  ${
    eumoi.1 $e |- E! x ph $.
    $( "At most one" inferred from existential uniqueness. $)
    eumoi $p |- E* x ph $=
      ( weu wmo eumo ax-mp ) ABDABECABFG $.
      $( [5-Apr-1995] $)
  $}

  $( Existence in terms of "at most one" and uniqueness. $)
  exmoeu $p |- ( E. x ph <-> ( E* x ph -> E! x ph ) ) $=
    ( wex wmo weu wi df-mo biimp com12 biimpr euex imim12i peirce syl impbi )
    ABCZABDZABEZFZQPRQPRFZABGZHISTPFPTQRPQTUAJABKLPRMNO $.
    $( [17-Apr-2004] $) $( [5-Apr-2004] $)

  $( Existence implies "at most one" is equivalent to uniqueness. $)
  exmoeu2 $p |- ( E. x ph -> ( E* x ph <-> E! x ph ) ) $=
    ( weu wex wmo eu5 baibr ) ABCABDABEABFG $.
    $( [18-Apr-2004] $) $( [5-Apr-2004] $)

  $( Absorption of existence condition by "at most one". $)
  moabs $p |- ( E* x ph <-> ( E. x ph -> E* x ph ) ) $=
    ( wex weu wi wmo pm5.4 df-mo imbi2i 3bitr4r ) ABCZKABDZEZEMKABFZENKLGNMKABH
    ZIOJ $.
    $( [4-Nov-2002] $)

  $( Something exists or at most one exists. $)
  exmo $p |- ( E. x ph \/ E* x ph ) $=
    ( wex wmo wn weu wi pm2.21 df-mo sylibr orri ) ABCZABDZLELABFZGMLNHABIJK $.
    $( [8-Mar-1995] $)

  ${
    $d x y $.  $d y ph $.  $d y ps $.
    $( "At most one" is preserved through implication (notice wff reversal). $)
    immo $p |- ( A. x ( ph -> ps ) -> ( E* x ps -> E* x ph ) ) $=
      ( vy wi wal weq wex wmo imim1 19.20ii 19.22dv ax-17 mo2 3imtr4g ) ABEZCFZ
      BCDGZEZCFZDHAREZCFZDHBCIACIQTUBDPSUACABRJKLBCDBDMNACDADMNO $.
      $( [22-Apr-1995] $)
  $}

  ${
    immoi.1 $e |- ( ph -> ps ) $.
    $( "At most one" is preserved through implication (notice wff reversal). $)
    immoi $p |- ( E* x ps -> E* x ph ) $=
      ( wi wmo immo mpg ) ABEBCFACFECABCGDH $.
      $( [16-Feb-2006] $) $( [15-Feb-2006] $)
  $}

  ${
    $d x y $.  $d x y ph $.  $d y ps $.
    $( Move antecedent outside of "at most one". $)
    moimv $p |- ( E* x ( ph -> ps ) -> ( ph -> E* x ps ) ) $=
      ( vy wi wmo weq wal wex ax-1 a1i imim1d 19.20dv 19.22dv ax-17 mo2
      3imtr4g com12 ) AABEZCFZBCFZASCDGZEZCHZDIBUBEZCHZDITUAAUDUFDAUCUECABSUBBS
      EABAJKLMNSCDSDOPBCDBDOPQR $.
      $( [28-Jul-1995] $)
  $}

  $( Uniqueness implies "at most one" through implication. $)
  euimmo $p |- ( A. x ( ph -> ps ) -> ( E! x ps -> E* x ph ) ) $=
    ( wi wal wmo weu immo eumo syl5 ) ABDCEBCFACFBCGABCHBCIJ $.
    $( [22-Apr-1995] $)

  $( Add existential uniqueness quantifiers to an implication.  Note the
     reversed implication in the antecedent. $)
  euim $p |- ( ( E. x ph /\ A. x ( ph -> ps ) ) -> ( E! x ps -> E! x ph ) ) $=
    ( wex wi wal wa weu wmo euimmo adantl wb exmoeu2 adantr sylibd ) ACDZABECFZ
    GBCHZACIZACHZQRSEPABCJKPSTLQACMNO $.
    $( [20-Oct-2005] $) $( [19-Oct-2005] $)

  $( "At most one" is still the case when a conjunct is added. $)
  moan $p |- ( E* x ph -> E* x ( ps /\ ph ) ) $=
    ( wa pm3.27 immoi ) BADACBAEF $.
    $( [22-Apr-1995] $)

  ${
    moani.1 $e |- E* x ph $.
    $( "At most one" is still true when a conjunct is added. $)
    moani $p |- E* x ( ps /\ ph ) $=
      ( wmo wa moan ax-mp ) ACEBAFCEDABCGH $.
      $( [9-Mar-1995] $)
  $}

  $( "At most one" is still the case when a disjunct is removed. $)
  moor $p |- ( E* x ( ph \/ ps ) -> E* x ph ) $=
    ( wo orc immoi ) AABDCABEF $.
    $( [19-Apr-2004] $) $( [5-Apr-2004] $)

  $( "At most one" imports disjunction to conjunction. $)
  mooran1 $p |- ( ( E* x ph \/ E* x ps ) -> E* x ( ph /\ ps ) ) $=
    ( wmo wa moan ancom mobii sylib jaoi ) ACDZABEZCDZBCDKBAEZCDMABCFNLCBAGHIBA
    CFJ $.
    $( [20-Apr-2004] $) $( [5-Apr-2004] $)

  $( "At most one" exports disjunction to conjunction. $)
  mooran2 $p |- ( E* x ( ph \/ ps ) -> ( E* x ph /\ E* x ps ) ) $=
    ( wo wmo moor orcom mobii sylbi jca ) ABDZCEZACEBCEZABCFLBADZCEMKNCABGHBACF
    IJ $.
    $( [21-Apr-2004] $) $( [5-Apr-2004] $)

  ${
    $d x y $.  $d y ph $.  $d y ps $.
    moanim.1 $e |- ( ph -> A. x ph ) $.
    $( Introduction of a conjunct into "at most one" quantifier. $)
    moanim $p |- ( E* x ( ph /\ ps ) <-> ( ph -> E* x ps ) ) $=
      ( vy wa weq wi wal wex wmo impexp albii 19.21 bitr exbii ax-17 mo2
      imbi2i 19.37v bitr4 3bitr4 ) ABFZCEGZHZCIZEJABUDHZCIZHZEJZUCCKABCKZHZUFUI
      EUFAUGHZCIUIUEUMCABUDLMAUGCDNOPUCCEUCEQRULAUHEJZHUJUKUNABCEBEQRSAUHETUAUB
      $.
      $( [3-Dec-2001] $)

    $( Introduction of a conjunct into uniqueness quantifier. $)
    euan $p |- ( E! x ( ph /\ ps ) <-> ( ph /\ E! x ps ) ) $=
      ( wa wex wmo weu 19.42 wi moanim anbi2i abai bitr4 anbi12i anass an4
      3bitr4 eu5 anabs1 exbii 19.41 bitr3 anbi1i bitr pm4.24 ) ABEZCFZAEZUGCGZE
      ZAAEZBCFZBCGZEZEZUGCHZABCHZEUHAUJEZEAUMEZAUNEZEUKUPUHUTUSVAABCDIUSAAUNJZE
      VAUJVBAABCDKLAUNMNOUHAUJPAAUMUNQRUQUHUJEUKUGCSUHUIUJUHUGAEZCFUIVCUGCABTUA
      UGACDUBUCUDUEAULURUOAUFBCSOR $.
      $( [20-Feb-2005] $) $( [19-Feb-2005] $)
  $}

  ${
    $d x y ph $.  $d y ps $.
    $( Introduction of a conjunct into "at most one" quantifier. $)
    moanimv $p |- ( E* x ( ph /\ ps ) <-> ( ph -> E* x ps ) ) $=
      ( ax-17 moanim ) ABCACDE $.
      $( [23-Mar-1995] $)
  $}

  $( Nested "at most one" and uniqueness quantifiers. $)
  moaneu $p |- E* x ( ph /\ E! x ph ) $=
    ( weu wa wmo wi eumo hbeu1 moanim mpbir ancom mobii ) AABCZDZBEMADZBEZPMABE
    FABGMABABHIJNOBAMKLJ $.
    $( [1-Mar-2006] $) $( [25-Jan-2006] $)

  $( Nested "at most one" quantifiers. $)
  moanmo $p |- E* x ( ph /\ E* x ph ) $=
    ( wmo wa wi id hbmo1 moanim mpbir ancom mobii ) AABCZDZBCLADZBCZOLLELFLABAB
    GHIMNBALJKI $.
    $( [26-Jan-2006] $) $( [25-Jan-2006] $)

  ${
    $d x ph $.
    $( Introduction of a conjunct into uniqueness quantifier. $)
    euanv $p |- ( E! x ( ph /\ ps ) <-> ( ph /\ E! x ps ) ) $=
      ( ax-17 euan ) ABCACDE $.
      $( [23-Mar-1995] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.
    $( "At most one" picks a variable value, eliminating an existential
       quantifier. $)
    mopick $p |- ( ( E* x ph /\ E. x ( ph /\ ps ) ) -> ( ph -> ps ) ) $=
      ( vy wa wex wmo wi wsb ax-17 hbs1 hban weq sbequ12 anbi12d cbvex sbequ2
      imim2i exp3a com4t imp wal mo3 ax-4 a4s sylbi syl5 19.23aiv impcom ) ABEZ
      CFZACGZABHZUKACDIZBCDIZEZDFULUMHZUJUPCDUJDJUNUOCACDKBCDKLCDMZAUNBUOACDNBC
      DNOPUPUQDUPAUNEZURHZUMULUNUOUTUMHUTAUNUOBUTAUNUOBHZURVAUSBCDQRSTUAULUTDUB
      ZCUBUTACDADJUCVBUTCUTDUDUEUFUGUHUFUI $.
      $( [27-Jan-1997] $)
  $}

  $( Existential uniqueness "picks" a variable value for which another wff is
     true.  If there is only one thing ` x ` such that ` ph ` is true, and
     there is also an ` x ` (actually the same one) such that ` ph ` and ` ps `
     are both true, then ` ph ` implies ` ps ` regardless of ` x ` .  This
     theorem, which apparently does not appear explicitly in the literature,
     can be quite useful because it lets us eliminate existential quantifiers
     in a hypothesis. $)
  eupick $p |- ( ( E! x ph /\ E. x ( ph /\ ps ) ) -> ( ph -> ps ) ) $=
    ( wmo wa wex wi weu mopick eumo sylan ) ACDABECFABGACHABCIACJK $.
    $( [10-Jul-1994] $)

  $( Existential uniqueness "pick" showing wff equivalence. $)
  eupickb $p |- ( ( E! x ph /\ E! x ps /\ E. x ( ph /\ ps ) ) ->
               ( ph <-> ps ) ) $=
    ( weu wa wex w3a wi eupick 3adant2 3simpc pm3.22 19.22i anim2i 3syl impbid
    ) ACDZBCDZABEZCFZGZABQTABHRABCIJUARTERBAEZCFZEBAHQRTKTUCRSUBCABLMNBACIOP $.
    $( [25-Nov-1994] $)

  $( "At most one" can show the existence of a common value.  In this case we
     can infer existence of conjunction from a conjunction of existence, and
     it is one way to achieve the converse of ~ 19.40 . $)
  mopick2 $p |- ( ( E* x ph /\ E. x ( ph /\ ps ) /\ E. x ( ph /\ ch ) ) ->
                E. x ( ph /\ ps /\ ch ) ) $=
    ( wmo wa wex w3a pm3.26 19.22i 3ad2ant2 hbmo1 hbe1 hb3an wi mopick anim12i
    3anass anandi bitr jcab 3imtr4 ancld syl6ibr 19.22d mpd ) ADEZABFZDGZACFZDG
    ZHZADGZABCHZDGUIUGUMUKUHADABIJKULAUNDUGUIUKDADLUHDMUJDMNULAABCFZFUNULAUOUGU
    IFZUGUKFZFZABOZACOZFULAUOOUPUSUQUTABDPACDPQULUGUIUKFFURUGUIUKRUGUIUKSTABCUA
    UBUCABCRUDUEUF $.
    $( [22-Apr-2004] $) $( [5-Apr-2004] $)

  $( Introduce or eliminate a disjunct in a uniqueness quantifier. $)
  euor2 $p |- ( -. E. x ph -> ( E! x ( ph \/ ps ) <-> E! x ps ) ) $=
    ( wex wn wo weu wmo wa euex 19.43 sylib ord com12 wi eumo orcom mobii moor
    sylbi syl a1i jcad eu5 syl6ibr hbe1 euor olc 19.22i wal 19.8a orim1i
    ax-gen euim mpan2 3syl adantl mpd ex impbid ) ACDZEZABFZCGZBCGZVBVDBCDZBCHZ
    IVEVBVDVFVGVDVBVFVDVAVFVDVCCDZVAVFFVCCJABCKLMNVDVGOVBVDVCCHZVGVCCPVIBAFZCHV
    GVCVJCABQRBACSTUAUBUCBCUDUEVBVEVDVBVEIVABFZCGZVDVABCACUFUGVEVLVDOZVBVEVFVHV
    MBCJBVCCBAUHUIVHVCVKOZCUJVMVNCAVABACUKULUMVCVKCUNUOUPUQURUSUT $.
    $( [25-Oct-2005] $) $( [21-Oct-2005] $)

  ${
    moexex.1 $e |- ( ph -> A. y ph ) $.
    $( "At most one" double quantification. $)
    moexex $p |- ( ( E* x ph /\ A. x E* y ps ) -> E* y E. x ( ph /\ ps ) ) $=
      ( wmo wal wa wex wi hbmo1 hba1 hbe1 hbmo hbim mopick ex com3r 19.21ad
      immo a4sd syl6 19.23ai wn hbex pm3.26 19.22i con3i exmo ori syl a1d
      pm2.61i imp ) ACFZBDFZCGZABHZCIZDFZACIZUOUQUTJZJZAVCCUOVBCACKUQUTCUPCLUSC
      DURCMNOOAUOUSBJZDGZVBAUOVDDEADCENUOUSABUOUSABJABCPQRSVEUPUTCUSBDTUAUBUCVA
      UDZVBUOVFUTUQVFUSDIZUDUTVGVAUSVADADCEUEURACABUFUGUCUHVGUTUSDUIUJUKULULUMU
      N $.
      $( [3-Dec-2001] $)
  $}

  ${
    $d y ph $.
    $( "At most one" double quantification. $)
    moexexv $p |- ( ( E* x ph /\ A. x E* y ps ) -> E* y E. x ( ph /\ ps ) ) $=
      ( ax-17 moexex ) ABCDADEF $.
      $( [26-Jan-1997] $)
  $}

  $( Double quantification with "at most one". $)
  2moex $p |- ( E* x E. y ph -> A. y E* x ph ) $=
    ( wex wmo hbe1 hbmo 19.8a immoi 19.21ai ) ACDZBEABECKCBACFGAKBACHIJ $.
    $( [3-Dec-2001] $)

  $( Double quantification with existential uniqueness. $)
  2euex $p |- ( E! x E. y ph -> E. y E! x ph ) $=
    ( wex weu wmo wa eu5 hbe1 hbmo 19.41 biimpr excom sylanb 2moex 19.21bi
    anim2i sylibr 19.22i syl sylbi ) ACDZBEUBBDZUBBFZGZABEZCDZUBBHUEABDZUDGZCDZ
    UGUHCDZUDUJUCUJUKUDGUHUDCUBCBACIJKLABCMNUIUFCUIUHABFZGUFUDULUHUDULCABCOPQAB
    HRSTUA $.
    $( [3-Dec-2001] $)

  $( Double quantification with existential uniqueness and "at most one". $)
  2eumo $p |- ( E! x E* y ph -> E* x E! y ph ) $=
    ( weu wmo wi euimmo eumo mpg ) ACDZACEZFKBDJBEFBJKBGACHI $.
    $( [3-Dec-2001] $)

  $( Double existential uniqueness. $)
  2eu2ex $p |- ( E! x E! y ph -> E. x E. y ph ) $=
    ( weu wex euex 19.22i syl ) ACDZBDIBEACEZBEIBFIJBACFGH $.
    $( [3-Dec-2001] $)

  $( A condition allowing swap of "at most one" and existential quantifiers. $)
  2moswap $p |- ( A. x E* y ph -> ( E* x E. y ph -> E* y E. x ph ) ) $=
    ( wmo wal wex wa hbe1 moexex expcom 19.8a pm4.71ri exbii mobii syl6ibr ) AC
    DBEZACFZBDZQAGZBFZCDZABFZCDRPUAQABCACHIJUBTCASBAQACKLMNO $.
    $( [11-Apr-2004] $) $( [10-Apr-2004] $)

  $( A condition allowing swap of uniqueness and existential quantifiers. $)
  2euswap $p |- ( A. x E* y ph -> ( E! x E. y ph -> E! y E. x ph ) ) $=
    ( wmo wal wex wa weu wi excomim a1i 2moswap anim12d eu5 3imtr4g ) ACDBEZACF
    ZBFZQBDZGABFZCFZTCDZGQBHTCHPRUASUBRUAIPABCJKABCLMQBNTCNO $.
    $( [13-Apr-2004] $) $( [10-Apr-2004] $)

  $( Double existential uniqueness implies double uniqueness quantification. $)
  2exeu $p |- ( ( E! x E. y ph /\ E! y E. x ph ) -> E! x E! y ph ) $=
    ( wex wmo wa weu hbe1 hbmo 19.41 19.8a immoi anim2i 19.22i sylbir excom
    sylanb pm3.26 adantl anim12i ancoms eu5 anbi12i exbii mobii bitr 3imtr4 )
    ACDZBDZUHBEZFZABDZCDZULCEZFZFUHACEZFZBDZUQBEZFZUHBGZULCGZFACGZBGZUOUKUTUOUR
    UKUSUIUNURUMUIUNFUHUNFZBDURUHUNBULBCABHIJVEUQBUNUPUHAULCABKLMNOACBPQUJUSUIU
    QUHBUHUPRLSTUAVAUKVBUOUHBUBULCUBUCVDVCBDZVCBEZFUTVCBUBVFURVGUSVCUQBACUBZUDV
    CUQBVHUEUCUFUG $.
    $( [3-Dec-2001] $)

  ${
    $d x y z w v u $.  $d z w v u ph $.
    $( Two equivalent expressions for double "at most one". $)
    2mo $p |- ( E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) <->
              A. x A. y A. z A. w ( ( ph /\ [ z / x ] [ w / y ] ph ) ->
                   ( x = z /\ y = w ) ) ) $=
      ( vv vu weq wa wi wal wex wsb equequ2 bi2anan9 imbi2d 2albidv cbvex2v
      ax-17 hbs1 hbim hbsb sbequ12 sylan9bbr equequ1 imbi12d cbval2 biimp
      ancli alcom aaan albii bitr hbal sylibr prth equtr2 anim12i an4s syl6
      19.20i2 syl 19.23aivv sylbir alrot4 19.20 19.20ii sylbi 19.22 19.20i
      3syl 19.21ai 19.22i2 syl5com impexp bi2.04 2albii syl5ib wn alnex hbne
      negbid biimpr pm2.21 19.8a 19.23bi a1d pm2.61i impbi ) ABDHZCEHZIZJZCKZBK
      ZELZDLZAACEMZBDMZIZWLJZEKDKZCKBKZWQABFHZCGHZIZJZCKZBKZGLFLXCXIWOFGDEFDHZG
      EHZIZXGWMBCXLXFWLAXJXDWJXKXEWKFDBNGECNOPQRXIXCFGXIXGWSDFHZEGHZIZJZIZEKZDK
      ZCKZBKZXCXIXIXPEKZDKZIZYAXIYCXIYCXGXPBCDEXGDSXGESZWSXOBWRBDTZXOBSUAZWSXOC
      WRBDCACETUBZXOCSUAZWLAWSXFXOWKAWRWJWSACEUCWRBDUCUDZWJXDXMWKXEXNBDFUECEGUE
      OUFUGUHUIYAXHYBIZDKZBKYDXTYLBXTXRCKZDKYLXRCDUJYMYKDXGXPCEYEYIUKULUMULXHYB
      BDXHDSXPBEYGUNUKUMUOXSXBBCXQXADEXQWTXFXOIWLAXFWSXOUPXDXMXEXNWLXDXMIWJXEXN
      IWKBDFUQCEGUQURUSUTVAVAVBVCVDWSELZDLZXCWQJYOWSWMJZEKDKZCKBKZWQXCYRWSCKZBK
      ZELZDLZWQYOYRYTWOJZEKZDKZUUAWPJZDKUUBWQJYRYPCKZBKZEKDKUUEYPBCDEVEUUHUUCDE
      UUGYSWNBWSWMCVFVGVAVHUUDUUFDYTWOEVIVJUUAWPDVIVKWSYTDEWSYSBYFYHVLVMVNXBYQB
      CXAYPDEXAAWSWLJJYPAWSWLVOAWSWLVPUMVQVQVRYOVSZWQXCUUIWSVSZEKZDKZWQUULYNVSZ
      DKUUIUUKUUMDWSEVTULYNDVTUMUULAVSZCKBKZWOWQUUOUULUUNUUJBCDEUUNDSUUNESWSBYF
      WAWSCYHWAWLAWSYJWBUGWCUUNWMBCAWLWDVAWOWQEWPDWEWFVKVDWGWHWI $.
      $( [7-Feb-2005] $) $( [2-Feb-2005] $)
  $}

  ${
    $d z w ph $.  $d x y ps $.  $d x y z w $.
    2mos.1 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Double "exists at most one" with implicit substitution. $)
    2mos $p  |- ( E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) <->
             A. x A. y A. z A. w ( ( ph /\ ps ) -> ( x = z /\ y = w ) ) ) $=
      ( weq wa wi wal wex wsb 2mo ax-17 sb19.21 wb expcom pm5.74d sbie bitr3
      pm5.74ri anbi2i imbi1i 2albii bitr ) ACEHZDFHZIZJDKCKFLELAADFMZCEMZIZUIJZ
      FKEKZDKCKABIZUIJZFKEKZDKCKACDEFNUNUQCDUMUPEFULUOUIUKBAUJBCEBCOUGUJBUGUJJU
      GAJZDFMUGBJZUGADFUGDOPURUSDFUSDOUHUGABUGUHABQGRSTUAUBTUCUDUEUEUF $.
      $( [17-May-2005] $) $( [10-Feb-2005] $)
  $}

  $( Double existential uniqueness.  This theorem shows a condition under
     which a "naive" definition matches the correct one. $)
  2eu1 $p |- ( A. x E* y ph ->
        ( E! x E! y ph <-> ( E! x E. y ph /\ E! y E. x ph ) ) ) $=
    ( wmo wal weu wex wa wi eu5 exbii mobii anbi12i bitr pm3.27bd ax-4 anim2i
    ancoms immoi hba1 moanim sylib ancrd 2moswap com12 imdistani syl6 syl
    2eu2ex excom jca jctild an4 syl6ibr 2exeu impbid1 ) ACDZBEZACFZBFZACGZBFZAB
    GZCFZHZUTURVEUTURVABGZVCCGZHZVABDZVCCDZHZHZVEUTURVKVHUTVAUQHZBDZURVKIUTVMBG
    ZVNUTUSBGZUSBDZHVOVNHUSBJVPVOVQVNUSVMBACJZKUSVMBVRLMNOVNURVIURHVKVNURVIVNUR
    VAHZBDURVIIVSVMBVAURVMURUQVAUQBPQRSURVABUQBTUAUBUCVIURVJURVIVJABCUDUEUFUGUH
    UTVFVGABCUIZUTVFVGVTABCUJUBUKULVEVFVIHZVGVJHZHVLVBWAVDWBVABJVCCJMVFVIVGVJUM
    NUNUEABCUOUP $.
    $( [3-Dec-2001] $)

  $( Double existential uniqueness. $)
  2eu2 $p |- ( E! y E. x ph -> ( E! x E! y ph <-> E! x E. y ph ) ) $=
    ( wex weu wmo wal wi eumo 2moex wa 2eu1 pm3.26 syl6bi 3syl 2exeu expcom
    impbid ) ABDZCEZACEBEZACDBEZTSCFACFBGZUAUBHSCIACBJUCUAUBTKUBABCLUBTMNOUBTUA
    ABCPQR $.
    $( [3-Dec-2001] $)


  $( Double existential uniqueness. $)
  2eu3 $p |- ( A. x A. y ( E* x ph \/ E* y ph ) ->
 ( ( E! x E! y ph /\ E! y E! x ph ) <-> ( E! x E. y ph /\ E! y E. x ph ) ) ) $=
    ( wmo wo wal weu wa wex wb hbmo1 19.31 albii hbal 19.32 bitr wi 2eu1
    biimpd ancom syl6ib adantld adantrd jaoi 2exeu ancoms jca impbid1 sylbi )
    ABDZACDZECFZBFZUJCFZUKBFZEZACGBGZABGCGZHZACIBGZABICGZHZJUMUNUKEZBFUPULVCBUJ
    UKCACKLMUNUKBUJBCABKNOPUPUSVBUNUSVBQUOUNURVBUQUNURVAUTHZVBUNURVDACBRSVAUTTU
    AUBUOUQVBURUOUQVBABCRSUCUDVBUQURABCUEVAUTURACBUEUFUGUHUI $.
    $( [3-Dec-2001] $)

  ${
    $d x y z w $.  $d z w ph $.
    $( This theorem provides us with a definition of double existential
       uniqueness ("exactly one ` x ` and exactly one ` y ` ").  Naively one
       might think (incorrectly) that it could be defined by
       ` E! x E! y ph ` .  See ~ 2eu1 for a condition under which the naive
       definition holds and ~ 2exeu for a one-way implication.  See ~ 2eu5 and
       ~ 2eu8 for alternate definitions. $)
    2eu4 $p |- ( ( E! x E. y ph /\ E! y E. x ph ) <->
      ( E. x E. y ph /\ E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) ) ) $=
      ( wex weu wa weq wi wal ax-17 eu3 anbi12i an4 excom anbi2i anidm bitr
      hba1 19.3r jcab albii 19.26 3bitr4 ax-4 impbi alcom bitr4 19.23v 2albii
      hbe1 hbim aaan 3bitr 2exbii eeanv bitr2 ) ACFZBGZABFZCGZHUSBFZUSBDIZJZBKZ
      DFZHZVACFZVACEIZJZCKZEFZHZHVCVIHZVGVMHZHVCAVDVJHJZCKZBKZEFDFZHUTVHVBVNUSB
      DUSDLMVACEVAELMNVCVGVIVMOVOVCVPVTVOVCVCHVCVIVCVCACBPQVCRSVTVFVLHZEFDFVPVS
      WADEVSAVDJZCKZAVJJZBKZHZCKZBKZVEVKHZCKBKWAVSWCWDCKZBKZHZBKZWHWCBKZWKHZWNW
      KBKZHVSWMWKWPWNWKBWJBTUAQVSWCWJHZBKWOVRWQBVRWBWDHZCKWQVQWRCAVDVJUBUCWBWDC
      UDSUCWCWJBUDSWCWKBUDUEWGWLBWGWCCKZWECKZHWLWCWECUDWSWCWTWKWSWCWCCUFWBCTUGW
      DCBUHNSUCUIWFWIBCWCVEWEVKAVDCUJAVJBUJNUKVEVKBCUSVDCACULVDCLUMVAVJBABULVJB
      LUMUNUOUPVFVLDEUQURNUO $.
      $( [3-Dec-2001] $)

    $( An alternate definition of double existential uniqueness (see ~ 2eu4 ).
       A mistake sometimes made in the literature is to use ` E! x E! y ` to
       mean "exactly one ` x ` and exactly one ` y ` ."  (For example, see
       Proposition 7.53 of [TakeutiZaring] p. 53.)  It turns out that this is
       actually a weaker assertion, as can be seen by expanding out the formal
       definitions.  This theorem shows that the erroneous definition can be
       repaired by conjoining ` A. x E* y ph ` as an additional condition.  The
       correct definition apparently has never been published.  ( ` E* ` means
       "exists at most one.")  $)
    2eu5 $p |- ( ( E! x E! y ph /\ A. x E* y ph ) <->
      ( E. x E. y ph /\ E. z E. w A. x A. y ( ph -> ( x = z /\ y = w ) ) ) ) $=
      ( weu wmo wal wa wex weq wi 2eu1 pm5.32ri eumo adantl 2moex syl pm4.71i
      2eu4 3bitr2 ) ACFBFZACGBHZIACJZBFZABJZCFZIZUCIUHUDBJABDKCEKILCHBHEJDJIUCU
      BUHABCMNUHUCUHUFCGZUCUGUIUEUFCOPACBQRSABCDETUA $.
      $( [26-Oct-2003] $) $( [26-Oct-2003] $)
  $}

  ${
    $d x y z w v u $.  $d z w v u ph $.
    $( Two equivalent expressions for double existential uniqueness. $)
    2eu6 $p  |- ( ( E! x E. y ph /\ E! y E. x ph ) <->
               E. z E. w A. x A. y ( ph <-> ( x = z /\ y = w ) ) ) $=
      ( vu vv wex weu wa weq wi wal wb 2eu4 wsb 19.29r2 ax-17 hbs1 hbsb
      sbequ12 sylan9bbr cbvex2 equequ2 bi2anan9 imbi2d 2albidv cbvex2v hbim
      equequ1 imbi12d cbval2 2exbii 2mo 3bitr anbi12i 2albi ancom bitr equcom
      imbi2i impexp 2albii hban sbco2 sbcom2 sbbii 3bitr3r syl6bb anbi2d
      19.21-2 3bitr3 anbi2i abai 2sb6 anbi1i 3bitr2 bitr4 3imtr4 bi2 19.20i2
      19.22i2 2exsb sylibr bi1 jca impbi ) ACHZBIABHCIJWHBHZABDKZCEKZJZLZCMBMZE
      HDHZJZAWLNZCMBMZEHDHZABCDEOWPWSACEPZBDPZEHDHZXAXAEFPZDGPZJZDGKZEFKZJZLZFM
      GMZEMDMZJXAXJJZEHDHZWPWSXAXJDEQWIXBWOXKAXABCDEADRAERZWTBDSZWTBDCACESTZWKA
      WTWJXAACEUAWTBDUAUBZUCWOABGKZCFKZJZLZCMBMZFHGHXAXHLZEMDMZFHGHXKWNYBDEGFXH
      WMYABCXHWLXTAXFWJXRXGWKXSDGBUDEFCUDUEUFUGUHYBYDGFYAYCBCDEYADRYAERXAXHBXOX
      HBRZUIXAXHCXPXHCRZUIWLAXAXTXHXQWJXRXFWKXSXGBDGUJCEFUJUEUKULUMXADEGFUNUOUP
      WSWLALZCMBMZWNJZEHDHXMWRYIDEWRWNYHJYIAWLBCUQWNYHURUSUMXLYIDEXLXAXAWNLZJXA
      WNJYIXJYJXAXAAJZDBKZECKZJZLZCMBMXAWMLZCMBMXJYJYOYPBCYOYKWLLYPYNWLYKYLWJYM
      WKDBUTECUTUPVAXAAWLVBUSVCYOXIBCGFYOGRYOFRXEXHBXAXDBXOXCDGBXAEFBXOTTVDYEUI
      XEXHCXAXDCXPXCDGCXAEFCXPTTVDYFUIXTYKXEYNXHXTAXDXAXTAACFPZBGPZXDXSAYQXRYRA
      CFUAYQBGUAUBWTEFPZBDPZDGPYSBGPXDYRYSBGDYSDRVEYTXCDGWTEFBDVFVGYSYQBGACFEXN
      VEVGVHVIVJXRYLXFXSYMXGBGDUDCFEUDUEUKULXAWMBCXOXPVKVLVMXAWNVNXAYHWNABCDEVO
      VPVQUMVRVSWSWIWOWSYHEHDHWIWRYHDEWQYGBCAWLVTWAWBABCDEWCWDWRWNDEWQWMBCAWLWE
      WAWBWFWGUS $.
      $( [13-Feb-2005] $) $( [2-Feb-2005] $)
  $}

  $( Two equivalent expressions for double existential uniqueness. $)
  2eu7 $p  |- ( ( E! x E. y ph /\ E! y E. x ph ) <->
             E! x E! y ( E. x ph /\ E. y ph ) ) $=
    ( wex weu wa hbe1 hbeu euan ancom eubii 3bitr 3bitr4r ) ABDZCEZACDZFZBEOPBE
    ZFNPFZCEZBEROFOPBNBCABGHITQBTPNFZCEPOFQSUACNPJKPNCACGIPOJLKROJM $.
    $( [21-Feb-2005] $) $( [19-Feb-2005] $)

  $( Two equivalent expressions for double existential uniqueness.
     Curiously, we can put ` E! ` on either of the internal conjuncts but not
     both.  We can also commute ` E! x E! y ` using ~ 2eu7 . $)
  2eu8 $p  |- ( E! x E! y ( E. x ph /\ E. y ph ) <->
                E! x E! y ( E! x ph /\ E. y ph ) ) $=
    ( wex weu wa 2eu2 pm5.32i hbeu1 hbeu euan ancom eubii hbe1 3bitr 3bitr4r
    2eu7 3bitr3r ) ACDZBEZABEZCEZFZTABDZCEZFUASFZCEZBEZUDSFCEBETUBUEACBGHUBSFZB
    EUBTFUHUCUBSBUABCABIJKUGUIBUGSUAFZCESUBFUIUFUJCUASLMSUACACNKSUBLOMTUBLPABCQ
    R $.
    $( [24-Apr-2005] $) $( [20-Feb-2005] $)

  ${
    $d x y $.
    $( Two ways of expressing "only one thing exists."  The left-hand side
       requires only one variable to express this.  Both sides are false in
       set theory; see theorem ~ dtru . $)
    exists1 $p |- ( E! x x = x <-> A. x x = y ) $=
      ( weq wal wex wb weu equid tbt bicom bitr albii exbii hbae 19.9r df-eu
      3bitr4r ) ABCZADZBEAACZRFZADZBESTAGSUBBRUAARRTFUATRAHIRTJKLMSBABBNOTABPQ
      $.
      $( [6-Apr-2004] $) $( [5-Apr-2004] $)

    $( A condition implying that at least two things exist. $)
    exists2 $p |- ( ( E. x ph /\ E. x -. ph ) -> -. E! x x = x ) $=
      ( vy weq weu wex wn wa wal exists1 pm3.24 wi ax16 a5i 19.9t syl anim12d
      mtoi sylbi con2i ) BBDBEZABFZAGZBFZHZUABCDZBIZUEGBCJUGUEAUCHAKUGUBAUDUCUG
      AABILZBIUBALUFUHBABCMNABOPUGUCUCBILZBIUDUCLUFUIBUCBCMNUCBOPQRST $.
      $( [9-May-2004] $) $( [10-Apr-2004] $)
  $}

