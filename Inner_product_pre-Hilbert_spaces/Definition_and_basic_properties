$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CPreHil $.

  $( Extend class notation with the class of all complex inner product spaces
     (also called pre-Hilbert spaces). $)
  cphl $a class CPreHil $.

  ${
    $d g n s w x y $.
    $( Define the class of all complex inner product spaces.  An inner product
       space is a normed vector space whose norm satisfies the parallelogram
       law (a property that induces an inner product).  Based on Exercise 4(b)
       of [ReedSimon] p. 63.  The vector operation is ` g ` , the scalar
       product is ` s ` , and the norm is ` n ` .  An inner product space is
       also called a pre-Hilbert space. $)
    df-ph $a |- CPreHil = ( NrmCVec i^i { <. <. g , s >. , n >. |
          A. x e. ran g A. y e. ran g
       ( ( ( n ` ( x g y ) ) ^ 2 ) + ( ( n ` ( x g ( -u 1 s y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( n ` x ) ^ 2 ) + ( ( n ` y ) ^ 2 ) ) ) } ) $.

    $( Every complex inner product space is a normed complex vector space. $)
    phnv $p |- ( U e. CPreHil -> U e. NrmCVec ) $=
      ( vx vy vg vn vs cphl cnv cv co cfv c2 cexp c1 cneg caddc cmul wceq crn
      wral copab2 cin df-ph inss1 eqsstri sseli ) GHAGHBIZCIZDIZJEIZKLMJUGNOUHF
      IJUIJUJKLMJPJLUGUJKLMJUHUJKLMJPJQJRCUISZTBUKTDFEUAZUBHBCDEFUCHULUDUEUF $.
      $( [2-Apr-2007] $)

    $( The class of all complex inner product spaces is a relation. $)
    phrel $p |- Rel CPreHil $=
      ( vx cphl cnv wss wrel cv phnv ssriv nvrel relss mp2 ) BCDCEBEABCAFGHIBCJ
      K $.
      $( [2-Apr-2007] $)
  $}

  ${
    phnvi.1 $e |- U e. CPreHil $.
    $( Every complex inner product space is a normed complex vector space. $)
    phnvi $p |- U e. NrmCVec $=
      ( cphl wcel cnv phnv ax-mp ) ACDAEDBAFG $.
      $( [20-Nov-2007] $)
  $}

  ${
    $d g n s x y G $.  $d g n s x y N $.  $d g n s x y S $.  $d g n s x y X $.
    $d g n s Z $.
    isphg.1 $e |- X = ran G $.
    $( The predicate "is a complex inner product space."  An inner product
       space is a normed vector space whose norm satisfies the parallelogram
       law.  The vector (group) addition operation is ` G ` , the scalar
       product is ` S ` , and the norm is ` N ` .  An inner product space is
       also called a pre-Hilbert space. $)
    isphg $p |- ( ( G e. A /\ S e. B /\ N e. C ) ->
  ( <. <. G , S >. , N >. e. CPreHil <-> ( <. <. G , S >. , N >. e. NrmCVec /\
          A. x e. X A. y e. X
      ( ( ( N ` ( x G y ) ) ^ 2 ) + ( ( N ` ( x G ( -u 1 S y ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` x ) ^ 2 ) + ( ( N ` y ) ^ 2 ) ) ) ) ) ) $=
      ( vg vn vs wcel w3a cop cnv cv co cfv c2 cexp c1 cneg caddc cmul wceq
      crn wral copab2 wa cphl rneq syl6eqr opreq fveq2d opreq1d opreq12d
      eqeq1d raleqbidv opreq2d 2ralbidv fveq1 eqeq12d ralbidv eloprabg anbi2d
      cin df-ph eleq2i elin bitri syl5bb ) GCNFDNHENOZGFPHPZQNZVOARZBRZKRZSZLRZ
      TZUAUBSZVQUCUDZVRMRZSZVSSZWATZUAUBSZUESZUAVQWATZUAUBSZVRWATZUAUBSZUESZUFS
      ZUGZBVSUHZUIZAWRUIZKMLUJZNZUKZVPVQVRGSZHTZUAUBSZVQWDVRFSZGSZHTZUAUBSZUESZ
      UAVQHTZUAUBSZVRHTZUAUBSZUESZUFSZUGZBIUIZAIUIZUKVOULNZVNXBXTVPWTXDWATZUAUB
      SZVQWFGSZWATZUAUBSZUESZWPUGZBIUIZAIUIYCXHWATZUAUBSZUESZWPUGZBIUIZAIUIXTKM
      LGFHCDEVSGUGZWSYIAWRIYOWRGUHIVSGUMJUNZYOWQYHBWRIYPYOWJYGWPYOWCYCWIYFUEYOW
      BYBUAUBYOVTXDWAVQVRVSGUOUPUQYOWHYEUAUBYOWGYDWAVQWFVSGUOUPUQURUSUTUTWEFUGZ
      YHYMABIIYQYGYLWPYQYFYKYCUEYQYEYJUAUBYQYDXHWAYQWFXGVQGWDVRWEFUOVAUPUQVAUSV
      BWAHUGZYNXSAIYRYMXRBIYRYLXKWPXQYRYCXFYKXJUEYRYBXEUAUBXDWAHVCUQYRYJXIUAUBX
      HWAHVCUQURYRWOXPUAUFYRWLXMWNXOUEYRWKXLUAUBVQWAHVCUQYRWMXNUAUBVRWAHVCUQURV
      AVDVEVEVFVGYAVOQXAVHZNXCULYSVOABKLMVIVJVOQXAVKVLVM $.
      $( [2-Apr-2007] $)
  $}

  ${
    phop.2 $e |- G = ( +v ` U ) $.
    phop.4 $e |- S = ( .s ` U ) $.
    phop.6 $e |- N = ( norm ` U ) $.
    $( A complex inner product space in terms of ordered pair components. $)
    phop $p |- ( U e. CPreHil -> U = <. <. G , S >. , N >. ) $=
      ( cphl wcel c1st cfv c2nd cop wrel wceq phrel 1st2nd mpan cnv cvc phnv
      eqid nvvc vcrel vafval smfval opeq12i syl6eqr 3syl opeq1d nmfval opeq2i
      syl5eqr eqtrd ) BHIZBBJKZBLKZMZCAMZDMZHNUOBUROPBHQRUOUPDMUTURUOUPUSDUOBSI
      UPTIZUPUSOBUABUPUPUBUCVAUPUPJKZUPLKZMZUSTNVAUPVDOUDUPTQRCVBAVCBCEUEABFUFU
      GUHUIUJDUQUPBDGUKULUMUN $.
      $( [2-Apr-2007] $)
  $}

