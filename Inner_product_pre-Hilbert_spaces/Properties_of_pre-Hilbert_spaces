$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Properties of pre-Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y M $.  $d x y N $.  $d x y U $.
    $d x y X $.
    isph.1 $e |- X = ( BaseSet ` U ) $.
    isph.2 $e |- G = ( +v ` U ) $.
    isph.3 $e |- M = ( -v ` U ) $.
    isph.6 $e |- N = ( norm ` U ) $.
    $( The predicate "is an inner product space." $)
    isph $p |- ( U e. CPreHil <-> ( U e. NrmCVec /\ A. x e. X A. y e. X
      ( ( ( N ` ( x G y ) ) ^ 2 ) + ( ( N ` ( x M y ) ) ^ 2 ) ) = (
      2 x. ( ( ( N ` x ) ^ 2 ) + ( ( N ` y ) ^ 2 ) ) ) ) ) $=
      ( wcel wa co cfv c2 cexp caddc wceq cvv cphl cnv cv cmul pm4.71ri cns cop
      wral phnv wb eqid nvop eleq1 cneg cpv fvex eqeltri cnm bafval isphg mp3an
      nvmval 3expa fveq2d opreq1d opreq2d eqeq1d ralbidva pm5.32i anbi1d syl5bb
      c1 syl5rbb bitrd syl bianabs bitri ) CUALZCUBLZVRMVSAUCZBUCZDNFOPQNZVTWAE
      NZFOZPQNZRNZPVTFOPQNWAFOPQNRNUDNZSZBGUHZAGUHZMZVRVSCUIUEVSVRWJVSVRWJVSCDC
      UFOZUGFUGZSZVRWKUJWLCDFIWLUKZKULWNVRWMUALZWKCWMUAUMWPWMUBLZWBVTVLUNWAWLND
      NZFOZPQNZRNZWGSZBGUHZAGUHZMZWNWKDTLWLTLFTLWPXEUJDCUOOTICUOUPUQCUFUPFCUROT
      KCURUPUQABTTTWLDFGCDGHIUSUTVAWKVSXDMWNXEVSWJXDVSWIXCAGVSVTGLZMZWHXBBGXGWA
      GLZMZWFXAWGXIWEWTWBRXIWDWSPQXIWCWRFVSXFXHWCWRSVTWAWLCDEGHIWOJVBVCVDVEVFVG
      VHVHVIWNVSWQXDCWMUBUMVJVMVKVNVOVPVIVQ $.
      $( [1-Feb-2008] $)

    $( The parallelogram law for an inner product space. $)
    phpar2 $p |- ( ( U e. CPreHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A M B ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( vx co cfv c2 cexp caddc cmul wceq opreq1d vy cphl wcel w3a cv wral isph
      cnv wa simpr sylbi 3ad2ant1 opreq1 fveq2d opreq12d opreq2d eqeq12d opreq2
      wi fveq2 rcla42v 3adant1 mpd ) CUBUCZAGUCZBGUCZUDLUEZUAUEZDMZFNZOPMZVGVHE
      MZFNZOPMZQMZOVGFNZOPMZVHFNZOPMZQMZRMZSZUAGUFLGUFZABDMZFNZOPMZABEMZFNZOPMZ
      QMZOAFNZOPMZBFNZOPMZQMZRMZSZVDVEWCVFVDCUHUCZWCUIWCLUACDEFGHIJKUGWRWCUJUKU
      LVEVFWCWQUSVDWBWQAVHDMZFNZOPMZAVHEMZFNZOPMZQMZOWLVSQMZRMZSLUAABGGVGASZVOX
      EWAXGXHVKXAVNXDQXHVJWTOPXHVIWSFVGAVHDUMUNTXHVMXCOPXHVLXBFVGAVHEUMUNTUOXHV
      TXFORXHVQWLVSQXHVPWKOPVGAFUTTTUPUQVHBSZXEWJXGWPXIXAWFXDWIQXIWTWEOPXIWSWDF
      VHBADURUNTXIXCWHOPXIXBWGFVHBAEURUNTUOXIXFWOORXIVSWNWLQXIVRWMOPVHBFUTTUPUP
      UQVAVBVC $.
      $( [2-Mar-2008] $) $( [2-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y N $.  $d x y S $.  $d x y X $.
    phpar.1 $e |- X = ( BaseSet ` U ) $.
    phpar.2 $e |- G = ( +v ` U ) $.
    phpar.4 $e |- S = ( .s ` U ) $.
    phpar.6 $e |- N = ( norm ` U ) $.
    $( The parallelogram law for an inner product space. $)
    phpar $p |- ( ( U e. CPreHil /\ A e. X /\ B e. X ) ->
      ( ( ( N ` ( A G B ) ) ^ 2 ) + ( ( N ` ( A G ( -u 1 S B ) ) ) ^ 2 ) ) =
        ( 2 x. ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) ) $=
      ( wcel co cfv c2 cexp caddc cmul cvv opreq1d vx vy cphl cv cneg wceq wral
      w3a c1 cop c1st vafval fvex eqeltri c2nd smfval nmfval 3pm3.2i a1i eleq1d
      ibi cnv bafval isphg simplbda syl11anc 3ad2ant1 wi opreq1 fveq2d opreq12d
      phop fveq2 opreq2d eqeq12d opreq2 rcla42v 3adant1 mpd ) DUCLZAGLZBGLZUHUA
      UDZUBUDZEMZFNZOPMZWCUIUEZWDCMZEMZFNZOPMZQMZOWCFNZOPMZWDFNZOPMZQMZRMZUFZUB
      GUGUAGUGZABEMZFNZOPMZAWHBCMZEMZFNZOPMZQMZOAFNZOPMZBFNZOPMZQMZRMZUFZVTWAXA
      WBVTESLZCSLZFSLZUHZECUJFUJZUCLZXAXTVTXQXRXSEDUKNZUKNSDEIULYCUKUMUNCYCUONS
      CDJUPYCUOUMUNFDUONSDFKUQDUOUMUNURUSVTYBVTDYAUCCDEFIJKVLUTVAXTYBYAVBLXAUAU
      BSSSCEFGDEGHIVCVDVEVFVGWAWBXAXPVHVTWTXPAWDEMZFNZOPMZAWIEMZFNZOPMZQMZOXKWQ
      QMZRMZUFUAUBABGGWCAUFZWMYJWSYLYMWGYFWLYIQYMWFYEOPYMWEYDFWCAWDEVIVJTYMWKYH
      OPYMWJYGFWCAWIEVIVJTVKYMWRYKORYMWOXKWQQYMWNXJOPWCAFVMTTVNVOWDBUFZYJXIYLXO
      YNYFXDYIXHQYNYEXCOPYNYDXBFWDBAEVPVJTYNYHXGOPYNYGXFFYNWIXEAEWDBWHCVPVNVJTV
      KYNYKXNORYNWQXMXKQYNWPXLOPWDBFVMTVNVNVOVQVRVS $.
      $( [2-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y N $.  $d x y S $.  $d x y X $.
    ip1i.1 $e |- X = ( BaseSet ` U ) $.
    ip1i.2 $e |- G = ( +v ` U ) $.
    ip1i.4 $e |- S = ( .s ` U ) $.
    ip1i.7 $e |- P = ( .i ` U ) $.
    ip1i.9 $e |- U e. CPreHil $.
    ${
      ip1i.a $e |- A e. X $.
      ip1i.b $e |- B e. X $.
      ip1i.c $e |- C e. X $.
      ${
        ip1i.6 $e |- N = ( norm ` U ) $.
        ip0i.j $e |- J e. CC $.
        $( A slight variant of Equation 6.46 of [Ponnusamy] p. 362, where ` J `
           is either 1 or -1 to represent +-1. $)
        ip0i $p |- ( ( ( ( N ` ( ( A G B ) G ( J S C ) ) ) ^ 2 ) -
             ( ( N ` ( ( A G B ) G ( -u J S C ) ) ) ^ 2 ) ) +
             ( ( ( N ` ( ( A G ( -u 1 S B ) ) G ( J S C ) ) ) ^ 2 ) -
           ( ( N ` ( ( A G ( -u 1 S B ) ) G ( -u J S C ) ) ) ^ 2 ) ) ) =
                ( 2 x. ( ( ( N ` ( A G ( J S C ) ) ) ^ 2 ) -
                ( ( N ` ( A G ( -u J S C ) ) ) ^ 2 ) ) ) $=
          ( c2 co cfv cexp cneg cmin cmul caddc c1 2cn phnvi cnv cc nvscl mp3an
          wcel nvgcl nvcli recni sqcli negcli subdii wceq mulcli pnpcan2 eqtr4i
          cabl w3a c1st eqid nvvc ax-mp vafval vcabl 3pm3.2i bafval abl23 mp2an
          cvc fveq2i ax-1cn opreq12i cphl phpar adddii 3eqtri addsub4i 3eqtr2ri
          opreq1i ) UAAHCEUBZGUBZIUCZUAUDUBZAHUEZCEUBZGUBZIUCZUAUDUBZUFUBUGUBZU
          AWMUGUBZUABIUCZUAUDUBZUGUBZUHUBZUAWRUGUBZXCUHUBZUFUBZABGUBZWJGUBZIUCZ
          UAUDUBZAUIUEZBEUBZGUBZWJGUBZIUCZUAUDUBZUHUBZXHWOGUBZIUCZUAUDUBZXNWOGU
          BZIUCZUAUDUBZUHUBZUFUBXKYAUFUBXQYDUFUBUHUBWSWTXEUFUBZXGUAWMWRUJWLWLWK
          FIJKSFOUKZFULUPZAJUPZWJJUPZWKJUPZYGPYHHUMUPCJUPZYJYGTRHCEFJKMUNUOZAWJ
          FGJKLUQUOZURUSUTZWQWQWPFIJKSYGYHYIWOJUPZWPJUPZYGPYHWNUMUPYLYPYGHTVARW
          NCEFJKMUNUOZAWOFGJKLUQUOZURUSUTZVBWTUMUPXEUMUPXCUMUPXGYFVCUAWMUJYOVDU
          AWRUJYTVDUAXBUJXAXABFIJKSYGQURUSUTZVDWTXEXCVEUOVFXRXDYEXFUFXRWKBGUBZI
          UCZUAUDUBZWKXMGUBZIUCZUAUDUBZUHUBZUAWMXBUHUBUGUBZXDXKUUDXQUUGUHXJUUCU
          AUDXIUUBIGVGUPZYIBJUPZYJVHXIUUBVCFVIUCZVSUPZUUJYHUUMYGFUULUULVJVKVLGU
          ULFGLVMVNVLZYIUUKYJPQYMVOABWJGJFGJKLVPZVQVRVTWIXPUUFUAUDXOUUEIUUJYIXM
          JUPZYJVHXOUUEVCUUNYIUUPYJPYHXLUMUPUUKUUPYGUIWAVAQXLBEFJKMUNUOZYMVOAXM
          WJGJUUOVQVRVTWIWBFWCUPZYKUUKUUHUUIVCOYNQWKBEFGIJKLMSWDUOUAWMXBUJYOUUA
          WEWFYEWPBGUBZIUCZUAUDUBZWPXMGUBZIUCZUAUDUBZUHUBZUAWRXBUHUBUGUBZXFYAUV
          AYDUVDUHXTUUTUAUDXSUUSIUUJYIUUKYPVHXSUUSVCUUNYIUUKYPPQYRVOABWOGJUUOVQ
          VRVTWIYCUVCUAUDYBUVBIUUJYIUUPYPVHYBUVBVCUUNYIUUPYPPUUQYRVOAXMWOGJUUOV
          QVRVTWIWBUURYQUUKUVEUVFVCOYSQWPBEFGIJKLMSWDUOUAWRXBUJYTUUAWEWFWBXKXQY
          AYDXJXJXIFIJKSYGYHXHJUPZYJXIJUPYGYHYIUUKUVGYGPQABFGJKLUQUOZYMXHWJFGJK
          LUQUOURUSUTXPXPXOFIJKSYGYHXNJUPZYJXOJUPYGYHYIUUPUVIYGPUUQAXMFGJKLUQUO
          ZYMXNWJFGJKLUQUOURUSUTXTXTXSFIJKSYGYHUVGYPXSJUPYGUVHYRXHWOFGJKLUQUOUR
          USUTYCYCYBFIJKSYGYHUVIYPYBJUPYGUVJYRXNWOFGJKLUQUOURUSUTWGWH $.
          $( [23-Apr-2007] $)

        $( Lemma for ~ ip1i . $)
        ip1ilem $p |- ( ( ( A G B ) P C ) +
                         ( ( A G ( -u 1 S B ) ) P C ) ) = ( 2 x. ( A P C ) ) $=
          ( c4 co c1 cneg caddc cmul cdiv c2 cfv cexp cmin ci wcel wceq 4ipval2
          cnv phnvi mp3an opreq2i 2cn 4re recni ipcl mul12i nvgcl nvcli resqcli
          cc ax-1cn negcli nvscl subcli ax-icn mulcli adddii nvsid mp2an fveq2i
          ip0i opreq1i opreq12i eqtr4i add4i eqtr2i 3eqtri 3eqtr3ri addcli 4pos
          3eqtr3i gt0ne0ii divcan3i ) UAABGUBZCDUBZAUCUDZBEUBZGUBZCDUBZUEUBZUFU
          BZUAUGUBUAUHACDUBZUFUBZUFUBZUAUGUBWRXAWSXBUAUGUHUAWTUFUBZUFUBUHACGUBZ
          IUIZUHUJUBZAWNCEUBZGUBZIUIZUHUJUBZUKUBZULAULCEUBZGUBZIUIZUHUJUBZAULUD
          ZCEUBZGUBZIUIZUHUJUBZUKUBZUFUBZUEUBZUFUBZXBWSXCYCUHUFFUPUMZAJUMZCJUMZ
          XCYCUNFOUQZPRACDEFGIJKLMSNUOURUSUHUAWTUTUAVAVBZYEYFYGWTVHUMYHPRACDFJK
          NVCURZVDYDWLCGUBZIUIZUHUJUBZWLXGGUBZIUIZUHUJUBZUKUBZWPCGUBZIUIZUHUJUB
          ZWPXGGUBZIUIZUHUJUBZUKUBZUEUBZULWLXLGUBZIUIZUHUJUBZWLXQGUBZIUIZUHUJUB
          ZUKUBZUFUBZULWPXLGUBZIUIZUHUJUBZWPXQGUBZIUIZUHUJUBZUKUBZUFUBZUEUBZUEU
          BZYQUUMUEUBZUUDUVAUEUBZUEUBZWSYDUHXKUFUBZUHYBUFUBZUEUBUVCUHXKYBUTXFXJ
          XFXEXDFIJKSYHYEYFYGXDJUMYHPRACFGJKLVEURVFVGVBXJXIXHFIJKSYHYEYFXGJUMZX
          HJUMYHPYEWNVHUMZYGUVIYHUCVIVJZRWNCEFJKMVKURZAXGFGJKLVEURVFVGVBVLULYAV
          MXOXTXOXNXMFIJKSYHYEYFXLJUMZXMJUMYHPYEULVHUMYGUVMYHVMRULCEFJKMVKURZAX
          LFGJKLVEURVFVGVBXTXSXRFIJKSYHYEYFXQJUMZXRJUMYHPYEXPVHUMYGUVOYHULVMVJR
          XPCEFJKMVKURZAXQFGJKLVEURVFVGVBVLZVNVOUUEUVGUVBUVHUEWLUCCEUBZGUBZIUIZ
          UHUJUBZYPUKUBZWPUVRGUBZIUIZUHUJUBZUUCUKUBZUEUBUHAUVRGUBZIUIZUHUJUBZXJ
          UKUBZUFUBUUEUVGABCDEFGUCIJKLMNOPQRSVIVSUWBYQUWFUUDUEUWAYMYPUKUVTYLUHU
          JUVSYKIUVRCWLGYEYGUVRCUNYHRCEFJKMVPVQZUSVRVTVTUWEYTUUCUKUWDYSUHUJUWCY
          RIUVRCWPGUWKUSVRVTVTWAUWJXKUHUFUWIXFXJUKUWHXEUHUJUWGXDIUVRCAGUWKUSVRV
          TVTUSWIULUULUUTUEUBZUFUBULUHYAUFUBZUFUBUVBUVHUWLUWMULUFABCDEFGULIJKLM
          NOPQRSVMVSUSULUULUUTVMUUHUUKUUHUUGUUFFIJKSYHYEWLJUMZUVMUUFJUMYHYEYFBJ
          UMZUWNYHPQABFGJKLVEURZUVNWLXLFGJKLVEURVFVGVBUUKUUJUUIFIJKSYHYEUWNUVOU
          UIJUMYHUWPUVPWLXQFGJKLVEURVFVGVBVLZUUPUUSUUPUUOUUNFIJKSYHYEWPJUMZUVMU
          UNJUMYHYEYFWOJUMZUWRYHPYEUVJUWOUWSYHUVKQWNBEFJKMVKURAWOFGJKLVEURZUVNW
          PXLFGJKLVEURVFVGVBUUSUURUUQFIJKSYHYEUWRUVOUUQJUMYHUWTUVPWPXQFGJKLVEUR
          VFVGVBVLZVOULUHYAVMUTUVQVDWIWAWBYQUUDUUMUVAYMYPYMYLYKFIJKSYHYEUWNYGYK
          JUMYHUWPRWLCFGJKLVEURVFVGVBYPYOYNFIJKSYHYEUWNUVIYNJUMYHUWPUVLWLXGFGJK
          LVEURVFVGVBVLYTUUCYTYSYRFIJKSYHYEUWRYGYRJUMYHUWTRWPCFGJKLVEURVFVGVBUU
          CUUBUUAFIJKSYHYEUWRUVIUUAJUMYHUWTUVLWPXGFGJKLVEURVFVGVBVLULUULVMUWQVN
          ULUUTVMUXAVNWCWSUAWMUFUBZUAWQUFUBZUEUBUVFUAWMWQYIYEUWNYGWMVHUMYHUWPRW
          LCDFJKNVCURZYEUWRYGWQVHUMYHUWTRWPCDFJKNVCURZVOUXBUVDUXCUVEUEYEUWNYGUX
          BUVDUNYHUWPRWLCDEFGIJKLMSNUOURYEUWRYGUXCUVEUNYHUWTRWPCDEFGIJKLMSNUOUR
          WAWDWEWFVTWRUAWMWQUXDUXEWGYIUAVAWHWJZWKXAUAUHWTUTYJVNYIUXFWKWI $.
          $( [21-Apr-2007] $)
      $}

      $( Equation 6.47 of [Ponnusamy] p. 362. $)
      ip1i $p |- ( ( ( A G B ) P C ) +
                       ( ( A G ( -u 1 S B ) ) P C ) ) = ( 2 x. ( A P C ) ) $=
        ( c1 cnm cfv eqid ax-1cn ip1ilem ) ABCDEFGQFRSZHIJKLMNOPUCTUAUB $.
        $( [27-Apr-2007] $)
    $}

    ${
      ip2i.8 $e |- A e. X $.
      ip2i.9 $e |- B e. X $.
      $( Equation 6.48 of [Ponnusamy] p. 362. $)
      ip2i $p |- ( ( 2 S A ) P B ) = ( 2 x. ( A P B ) ) $=
        ( co cc0 caddc c1 wcel wceq c2 cneg cmul df-2 opreq1i cnv cc w3a ax-1cn
        phnvi 3pm3.2i nvdir mp2an nvsid opreq12i 3eqtri nvgcl mp3an ipcl eqtr4i
        addid1i cn0v cfv eqid nvrinv ip0l eqtri opreq2i ip1i 3eqtr2i ) UAADOZBC
        OZAAFOZBCOZPQOZVNARUBADOFOZBCOZQOUAABCOUCOVLVNVOVKVMBCVKRRQOZADOZRADOZV
        TFOZVMUAVRADUDUEEUFSZRUGSZWCAGSZUHVSWATELUJZWCWCWDUIUIMUKRRADEFGHIJULUM
        VTAVTAFWBWDVTATWEMADEGHJUNUMZWFUOUPUEVNWBVMGSZBGSZVNUGSWEWBWDWDWGWEMMAA
        EFGHIUQURNVMBCEGHKUSURVAUTVQPVNQVQEVBVCZBCOZPVPWIBCWBWDVPWITWEMADEFGWIH
        IJWIVDZVEUMUEWBWHWJPTWENBCEGWIHWKKVFUMVGVHAABCDEFGHIJKLMMNVIVJ $.
        $( [26-Apr-2007] $)
    $}

    ${
      ipdiri.8 $e |- A e. X $.
      ipdiri.9 $e |- B e. X $.
      ipdiri.10 $e |- C e. X $.
      $( Lemma for ~ ipdiri . $)
      ipdirilem $p |- ( ( A G B ) P C ) = ( ( A P C ) + ( B P C ) ) $=
        ( co c1 wceq mp2an c2 cdiv cmul cneg caddc 2cn 2ne0 recidi opreq1i wcel
        cnv cc phnvi reccli nvgcl mp3an 3pm3.2i nvsass nvsid 3eqtr3i nvscl ip2i
        w3a eqtr3i ax-1cn negcli ip1i nvdi cn0v cfv cabl wa c1st cvc eqid ax-mp
        nvvc vafval pm3.2i bafval abl4 smfval vc2 nvrinv opreq12i nv0rid 3eqtri
        vcabl opreq2i divcan1i 3eqtr2i nv0lid 3eqtrri mulcomi mul2negi 3eqtr4ri
        eqtri mulid1i ) ABGQZCDQZUARUAUBQZWSEQZCDQUCQZXBXAARUDZBEQZGQZEQZGQZCDQ
        ZXBXDXGEQZGQZCDQZUEQACDQZBCDQZUEQUAXBEQZCDQWTXCXOWSCDUAXAUCQZWSEQZRWSEQ
        ZXOWSXPRWSEUAUFUGUHUIFUKUJZUAULUJZXAULUJZWSHUJZVCXQXOSFMUMZXTYAYBUFUAUF
        UGUNZXSAHUJZBHUJZYBYCNOABFGHIJUOUPZUQUAXAWSEFHIKURTXSYBXRWSSYCYGWSEFHIK
        USTUTUIXBCDEFGHIJKLMXSYAYBXBHUJYCYDYGXAWSEFHIKVAUPZPVBVDXBXGCDEFGHIJKLM
        YHXSYAXFHUJZXGHUJYCYDXSYEXEHUJZYIYCNXSXDULUJZYFYJYCRVEVFZOXDBEFHIKVAUPZ
        AXEFGHIJUOUPZXAXFEFHIKVAUPPVGXIXMXLXNUEXHACDXAWSXFGQZEQZXHAXSYAYBYIVCYP
        XHSYCYAYBYIYDYGYNUQXAWSXFEFGHIJKVHTYPXAUAAEQZEQZXAUAUCQZAEQZAYOYQXAEYOA
        AGQZBXEGQZGQZYQFVIVJZGQZYQGVKUJZYEYFVLZYEYJVLYOUUCSFVMVJZVNUJZUUFXSUUIY
        CFUUHUUHVOVQVPZGUUHFGJVRZWHVPZYEYFNOVSZYEYJNYMVSABAXEGHFGHIJVTZWAUPUUAY
        QUUBUUDGUUIYEUUAYQSUUJNAEGUUHHUUKEFKWBZUUNWCTXSYFUUBUUDSYCOBEFGHUUDIJKU
        UDVOZWDTWEXSYQHUJZUUEYQSYCXSXTYEUUQYCUFNUAAEFHIKVAUPYQFGHUUDIJUUPWFTWGW
        IXSYAXTYEVCYTYRSYCYAXTYEYDUFNUQXAUAAEFHIKURTYTRAEQZAYSRAERUAVEUFUGWJZUI
        XSYEUURASYCNAEFHIKUSTWQWKVDUIXKBCDXAWSXDAEQZBGQZGQZEQZXBXAUVAEQZGQZBXKX
        SYAYBUVAHUJZVCUVCUVESYCYAYBUVFYDYGXSUUTHUJZYFUVFYCXSYKYEUVGYCYLNXDAEFHI
        KVAUPZOUUTBFGHIJUOUPUQXAWSUVAEFGHIJKVHTUVCXAUABEQZEQZRBEQZBUVBUVIXAEUVB
        AUUTGQZBBGQZGQZUVMUVIUUFUUGUVGYFVLUVBUVNSUULUUMUVGYFUVHOVSABUUTBGHUUNWA
        UPUVNUUDUVMGQZUVMUVLUUDUVMGXSYEUVLUUDSYCNAEFGHUUDIJKUUPWDTUIXSUVMHUJZUV
        OUVMSYCXSYFYFUVPYCOOBBFGHIJUOUPUVMFGHUUDIJUUPWLTWQUUIYFUVMUVISUUJOBEGUU
        HHUUKUUOUUNWCTWGWIYSBEQZUVJUVKXSYAXTYFVCUVQUVJSYCYAXTYFYDUFOUQXAUABEFHI
        KURTYSRBEUUSUIVDXSYFUVKBSYCOBEFHIKUSTZWMXJUVDXBGXDXAUCQZXFEQZXJUVDXSYKY
        AYIVCUVTXJSYCYKYAYIYLYDYNUQXDXAXFEFHIKURTUVTXAXDUCQZXFEQZXAXDXFEQZEQZUV
        DUVSUWAXFEXDXAYLYDWNUIXSYAYKYIVCUWBUWDSYCYAYKYIYDYLYNUQXAXDXFEFHIKURTUW
        CUVAXAEUWCUUTXDXEEQZGQZUVAXSYKYEYJVCUWCUWFSYCYKYEYJYLNYMUQXDAXEEFGHIJKV
        HTUWEBUUTGXDXDUCQZBEQZUVKUWEBUWGRBEUWGRRUCQRRRVEVEWORVEWRWQUIXSYKYKYFVC
        UWHUWESYCYKYKYFYLYLOUQXDXDBEFHIKURTUVRUTWIWQWIWGVDWIWPUIWEWK $.
        $( [26-Apr-2007] $)
    $}

    $( Distributive law for inner product.  Equation I3 of [Ponnusamy]
       p. 362. $)
    ipdiri $p |- ( ( A e. X /\ B e. X /\ C e. X ) ->
                 ( ( A G B ) P C ) = ( ( A P C ) + ( B P C ) ) ) $=
      ( wcel co caddc wceq cif opreq1 opreq2 cn0v opreq1d eqeq12d opreq12d eqid
      cfv opreq2d elimph ipdirilem dedth3h ) AHNZBHNZCHNZABGOZCDOZACDOZBCDOZPOZ
      QUKAFUAUFZRZBGOZCDOZUTCDOZUQPOZQUTULBUSRZGOZCDOZVCVECDOZPOZQVFUMCUSRZDOZU
      TVJDOZVEVJDOZPOZQABCUSUSUSAUTQZUOVBURVDVOUNVACDAUTBGSUBVOUPVCUQPAUTCDSUBU
      CBVEQZVBVGVDVIVPVAVFCDBVEUTGTUBVPUQVHVCPBVECDSUGUCCVJQZVGVKVIVNCVJVFDTVQV
      CVLVHVMPCVJUTDTCVJVEDTUDUCUTVEVJDEFGHIJKLMAFHUSIUSUEZMUHBFHUSIVRMUHCFHUSI
      VRMUHUIUJ $.
      $( [27-Apr-2007] $)

    ${
      $d j k A $.  $d j k B $.  $d j k C $.  $d j N $.  $d j k P $.
      $d j k S $.  $d j k X $.
      ipasslem1.b $e |- B e. X $.
      $( Lemma for ~ ipassi .  Show the inner product associative law for
         nonnegative integers. $)
      ipasslem1 $p |- ( ( N e. NN0 /\ A e. X ) ->
                      ( ( N S A ) P B ) = ( N x. ( A P B ) ) ) $=
        ( wcel co cmul wceq cc0 opreq1 vj vk cn0 cv wi c1 caddc wa cc nn0cn cnv
        phnvi nvscl mp3an1 sylan ipdiri mp3an3 sylancom ax-1cn w3a nvdir mp3an2
        mpan nvsid adantl opreq2d eqtrd opreq1d ipcl mp3an13 mulid2 syl 3eqtr4d
        sylan9eq adddir syl2an adantr eqtr4d exp31 a2d cn0v cfv eqid ip0l mp2an
        nv0 mul02 3eqtr4a eqeq12d imbi2d nn0indALT imp ) GUCOAHOZGADPZBCPZGABCP
        ZQPZRZWMUAUDZADPZBCPZWSWPQPZRZUEWMSADPZBCPZSWPQPZRZUEWMUBUDZADPZBCPZXHW
        PQPZRZUEWMXHUFUGPZADPZBCPZXMWPQPZRZUEWMWRUEUAUBGXHUCOZWMXLXQXRWMXLXQXRW
        MUHZXLUHXOXKUFWPQPZUGPZXPXSXLXOXJXTUGPZYAXSXIAFPZBCPZXJWPUGPZXOYBXRWMXI
        HOZYDYERZXRXHUIOZWMYFXHUJZEUKOZYHWMYFEMULZXHADEHIKUMUNUOYFWMBHOZYGNXIAB
        CDEFHIJKLMUPUQURXSXNYCBCXSXNXIUFADPZFPZYCXRYHWMXNYNRZYIYHUFUIOZWMYOUSYJ
        YHYPWMUTYOYKXHUFADEFHIJKVAVCVBUOXSYMAXIFWMYMARZXRYJWMYQYKADEHIKVDVCVEVF
        VGVHXSXTWPXJUGWMXTWPRZXRWMWPUIOZYRYJWMYLYSYKNABCEHILVIVJZWPVKVLVEVFVMXJ
        XKXTUGTVNXSXPYARZXLXRYHYSUUAWMYIYTYHYPYSUUAUSXHUFWPVOVBVPVQVRVSVTWMEWAW
        BZBCPZSXEXFYJYLUUCSRYKNBCEHUUBIUUBWCZLWDWEWMXDUUBBCYJWMXDUUBRYKADEHUUBI
        KUUDWFVCVHWMYSXFSRYTWPWGVLWHWSSRZXCXGWMUUEXAXEXBXFUUEWTXDBCWSSADTVHWSSW
        PQTWIWJWSXHRZXCXLWMUUFXAXJXBXKUUFWTXIBCWSXHADTVHWSXHWPQTWIWJWSXMRZXCXQW
        MUUGXAXOXBXPUUGWTXNBCWSXMADTVHWSXMWPQTWIWJWSGRZXCWRWMUUHXAWOXBWQUUHWTWN
        BCWSGADTVHWSGWPQTWIWJWKWL $.
        $( [27-Apr-2007] $)

      $( Lemma for ~ ipassi .  Show the inner product associative law for
         nonpositive integers. $)
      ipasslem2 $p |- ( ( N e. NN0 /\ A e. X ) ->
              ( ( -u N S A ) P B ) = ( -u N x. ( A P B ) ) ) $=
        ( wcel co cmul cc0 wceq cc cn0 wa cneg cmin c1 caddc nn0cn ax-1cn mpan2
        mulneg2 mulid1 negeqd eqtr2d adantr opreq1d negcli cnv w3a phnvi nvsass
        mpan mp3an2 eqtrd sylan nvscl mp3an12 ipasslem1 sylan2 opreq2d syl ipcl
        negcl mulcl syl2an negsub syl11anc adantl adddi syl111anc ipdiri mp3an3
        mp3an13 mpdan cn0v eqid nvrinv ip0l mp2an syl6eq eqtr3d mul01 sylan9eqr
        cfv mulneg1 3eqtr3rd 3eqtr2d wb mp3an1 subeq0 mpbid eqcomd ) GUAOZAHOZU
        BZGUCZABCPZQPZXEADPZBCPZXDXGXIUDPZRSZXGXISZXDXJXGGUEUCZADPZBCPZQPZUDPZX
        GXPUCZUFPZRXDXIXPXGUDXDXIGXNDPZBCPZXPXDXHXTBCXBGTOZXCXHXTSGUGZYBXCUBZXH
        GXMQPZADPZXTYDXEYEADYBXEYESXCYBYEGUEQPZUCZXEYBUETOYEYHSUHGUEUJUIYBYGGGU
        KULUMUNUOYBXMTOZXCYFXTSZUEUHUPZEUQOZYBYIXCURYJEMUSZGXMADEHIKUTVAVBVCVDU
        OXCXBXNHOZYAXPSYLYIXCYNYMYKXMADEHIKVEVFZXNBCDEFGHIJKLMNVGVHVCVIXDXGTOZX
        PTOZXSXQSXBXETOZXFTOZYPXCXBYBYRYCGVLVJZYLXCBHOZYSYMNABCEHILVKWBZXEXFVMV
        NZXBYBXOTOZYQXCYCXCYNUUDYOYLYNUUAUUDYMNXNBCEHILVKWBVJZGXOVMVNXGXPVOVPXD
        XEXFXOUFPZQPZXGXEXOQPZUFPZRXSXDYRYSUUDUUGUUISXBYRXCYTUNXCYSXBUUBVQXCUUD
        XBUUEVQXEXFXOVRVSXCXBUUGXERQPZRXCUUFRXEQXCAXNFPZBCPZUUFRXCYNUULUUFSZYOX
        CYNUUAUUMNAXNBCDEFHIJKLMVTWAWCXCUULEWDWMZBCPZRXCUUKUUNBCYLXCUUKUUNSYMAD
        EFHUUNIJKUUNWEZWFVAUOYLUUAUUORSYMNBCEHUUNIUUPLWGWHWIWJVIXBYRUUJRSYTXEWK
        VJWLXDUUHXRXGUFXBYBUUDUUHXRSXCYCUUEGXOWNVNVIWOWPXDYPXITOZXKXLWQUUCXDXHH
        OZUUQXBYRXCUURYTYLYRXCUURYMXEADEHIKVEWRVDYLUURUUAUUQYMNXHBCEHILVKWBVJXG
        XIWSVPWTXA $.
        $( [27-Apr-2007] $)

      $( Lemma for ~ ipassi .  Show the inner product associative law for all
         integers. $)
      ipasslem3 $p |- ( ( N e. ZZ /\ A e. X ) ->
              ( ( N S A ) P B ) = ( N x. ( A P B ) ) ) $=
        ( wcel cn0 co cmul wceq opreq1d cz cr cn wa wo elznn0nn ipasslem1 nnnn0
        cneg ipasslem2 sylan adantll cc recn negneg syl ad2antrr 3eqtr3d jaoian
        sylanb ) GUAOGPOZGUBOZGUIZUCOZUDZUEAHOZGADQZBCQZGABCQZRQZSZGUFVAVFVKVEA
        BCDEFGHIJKLMNUGVEVFUDVCUIZADQZBCQZVLVIRQZVHVJVDVFVNVOSZVBVDVCPOVFVPVCUH
        ABCDEFVCHIJKLMNUJUKULVBVNVHSVDVFVBVMVGBCVBVLGADVBGUMOVLGSGUNGUOUPZTTUQV
        BVOVJSVDVFVBVLGVIRVQTUQURUSUT $.
        $( [27-Apr-2007] $)


      $( Lemma for ~ ipassi .  Show the inner product associative law for
         positive integer reciprocals. $)
      ipasslem4 $p |- ( ( N e. NN /\ A e. X ) ->
                  ( ( ( 1 / N ) S A ) P B ) = ( ( 1 / N ) x. ( A P B ) ) ) $=
        ( wcel c1 co cmul wceq cc cn cdiv cc0 nncn nnne0 recid syl11anc opreq1d
        wa wne cnv phnvi ipcl mp3an13 mulid2 sylan9eq nvsid mpan adantr nnrecre
        syl recnd simpr nvsass syl111anc eqtr3d eqtr2d nnnn0 nvscl mp3an1 sylan
        w3a cn0 ipasslem1 adantl mulass 3eqtr3d wb mulcl syl2an syl112anc mpbid
        mulcan ) GUAOZAHOZUIZGPGUBQZADQZBCQZRQZGWGABCQZRQZRQZSZWIWLSZWFGWHDQZBC
        QZGWGRQZWKRQZWJWMWFWSWKWQWDWEWSPWKRQZWKWDWRPWKRWDGTOZGUCUJZWRPSGUDZGUEZ
        GUFUGZUHWEWKTOZWTWKSEUKOZWEBHOZXFEMULZNABCEHILUMUNZWKUOVAUPWFAWPBCWFWRA
        DQZAWPWDWEXKPADQZAWDWRPADXEUHXGWEXLASXIADEHIKUQURUPWFXAWGTOZWEXKWPSZWDX
        AWEXCUSZWDXMWEWDWGGUTVBZUSZWDWEVCXGXAXMWEVLXNXIGWGADEHIKVDURVEVFUHVGWFG
        VMOZWHHOZWQWJSWDXRWEGVHUSWDXMWEXSXPXGXMWEXSXIWGADEHIKVIVJVKZWHBCDEFGHIJ
        KLMNVNUGWFXAXMXFWSWMSXOXQWEXFWDXJVOGWGWKVPVEVQWFWITOZWLTOZXAXBWNWOVRWFX
        SYAXTXGXSXHYAXINWHBCEHILUMUNVAWDXMXFYBWEXPXJWGWKVSVTXOWDXBWEXDUSWIWLGWC
        WAWB $.
        $( [27-Apr-2007] $)

      $( Lemma for ~ ipassi .  Show the inner product associative law for
         rational numbers. $)
      ipasslem5 $p |- ( ( C e. QQ /\ A e. X ) ->
                  ( ( C S A ) P B ) = ( C x. ( A P B ) ) ) $=
        ( vj vk wcel co cmul wceq cq cv cdiv cn wrex cz wi elq wa w3a c1 cc zcn
        nnrecre recnd cnv ipcl mp3an13 mulass syl3an cc0 wne adantr nncn adantl
        phnvi nnne0 divrec syl111anc 3adant3 opreq1d id nvsass mpan eqtrd nvscl
        mp3an1 sylan ipasslem3 sylan2 ipasslem4 3adant1 opreq2d 3eqtrd 3eqtr4rd
        3impb opreq1 eqeq12d syl5ibrcom 3expia com23 r19.23aivv sylbi imp ) CUA
        QZAHQZCAERZBDRZCABDRZSRZTZWOCOUBZPUBZUCRZTZPUDUEOUFUEWPXAUGZOPCUHXEXFOP
        UFUDXBUFQZXCUDQZUIZWPXEXAXGXHWPXEXAUGXGXHWPUJZXAXEXDAERZBDRZXDWSSRZTXJX
        BUKXCUCRZSRZWSSRZXBXNWSSRZSRZXMXLXGXBULQZXHXNULQZWPWSULQZXPXRTXBUMZXHXN
        XCUNUOZFUPQZWPBHQYAFMVFZNABDFHILUQURXBXNWSUSUTXJXDXOWSSXGXHXDXOTZWPXIXS
        XCULQZXCVAVBZYFXGXSXHYBVCXHYGXGXCVDVEXHYHXGXCVGVEXBXCVHVIVJZVKXJXLXBXNA
        ERZERZBDRZXBYJBDRZSRZXRXJXKYKBDXJXKXOAERZYKXJXDXOAEYIVKXGXSXHXTWPWPYOYK
        TZYBYCWPVLYDXSXTWPUJYPYEXBXNAEFHIKVMVNUTVOVKXGXHWPYLYNTZXHWPUIXGYJHQZYQ
        XHXTWPYRYCYDXTWPYRYEXNAEFHIKVPVQVRYJBDEFGXBHIJKLMNVSVTWFXJYMXQXBSXHWPYM
        XQTXGABDEFGXCHIJKLMNWAWBWCWDWEXEWRXLWTXMXEWQXKBDCXDAEWGVKCXDWSSWGWHWIWJ
        WKWLWMWN $.
        $( [27-Apr-2007] $)
    $}

    ${
      $d q r s t u v w A $.  $d q r s t u v w B $.  $d s C $.
      $d q r s u w D $.  $d r s G $.  $d s u w J $.  $d q r s t u v w P $.
      $d q r s t u v w S $.  $d r s U $.  $d r s W $.  $d r s t u X $.
      ipasslem6.a $e |- A e. X $.
      ipasslem6.b $e |- B e. X $.
      ipasslem6.d $e |- D = ( abs o. - ) $.
      ipasslem6.8 $e |- C = ( IndMet ` U ) $.
      ipasslem6.j $e |- J = ( MetOpen ` D ) $.
      ipasslem6.f $e |- F = { <. w , v >. | ( w e. CC /\
              v = ( ( ( w S A ) P B ) - ( w x. ( A P B ) ) ) ) } $.
      $( Lemma for ~ ipassi .  Show that
         ` ( ( w S A ) P B ) - ( w x. ( A P B ) ) ` is continuous in ` w ` . $)
      ipasslem6 $p |- F e. ( J Cn J ) $=
        ( vu vt vs vr vq cv cc wcel co wceq wa copab ccn cmul ccom crn wss wral
        cnv phnvi nvscl mp3an13 rgen eqid oprex rnssopab mpbi fopabco ax-mp cme
        opreq1 w3a copn cnmet imsmet 3pm3.2i sm1cni ip1cni pm3.2i metcnco mp2an
        cfv eqeltrri ipcl mp3an cxp c1st c2nd cpr clt csup copab2 cnmetba mulcn
        cr opr1scn subcn opreq1d fvopab4 opreq12d eqeq2d pm5.32i opabbii eqtr4i
        cmin oprcn ) UEUJZUKULZUFUJZXKCHUMZDGUMZUNUOUEUFUPZLLUQUMZULXLXMXKCDGUM
        ZURUMZUNUOUEUFUPZXQULZJXQULUGUJZMULUHUJZYBDGUMZUNUOUGUHUPZXLXMXNUNUOUEU
        FUPZUSZXPXQYFUTMVAZYGXPUNXNMULZUEUKVBYHYIUEUKIVCULZXLCMULZYIIRVDZSXKCHI
        MNPVEVFVGUEUFUKMXNYFYFVHZXKCHVIZVJVKUEUFUGUHUFUKMXNYDXOYFYEXPYNYBDGVIXN
        DGVIYBXNDGVOYMYEVHZXPVHZVLVMFVNULZEVNULZYQVPYFLEVQWFZUQUMULZYEYSLUQUMUL
        ZUOYGXQULYQYRYQFUAVRZYJYRYLEIUBVSVMUUBVTYTUUAUEUFCFEHIYFLYSMNPUAUBUCYSV
        HZYMYLSWAUGUHDEFGIYEKYSLMNOQUBUAUUCUCYOYLTWBWCFEFYFYELYSLUCUUCUCWDWEWGX
        RUKULZYAYJYKDMULUUDYLSTCDGIMNQWHWIUGUHUIUEUFFFFYBUKUKWJZULYCUUEULUOUIUJ
        YBWKWFYCWKWFFUMYBWLWFYCWLWFFUMWMWSWNWOUNUOUGUHUIWPZXRXTLLUUFVQWFZURUKUK
        FUAWQZUUHUUBUUBUUBUUFVHZUCUCUUGVHZUGUHUIFUUFLUUGUAUUIUCUUJWRXTVHZWTVMUG
        UHUIABFFFUUFLXPXTJFLLLUUGXIUKUKUKUUHUUHUUHUUBUUBUUBUUBUCUCUCUUJUCUUIUGU
        HUIFUUFLUUGUAUUIUCUUJXAJAUJZUKULZBUJZUULCHUMZDGUMZUULXRURUMZXIUMZUNZUOZ
        ABUPUUMUUNUULXPWFZUULXTWFZXIUMZUNZUOZABUPUDUVEUUTABUUMUVDUUSUUMUVCUURUU
        NUUMUVAUUPUVBUUQXIUEUFUULXOUUPUKXPXKUULUNXNUUODGXKUULCHVOXBYPUUODGVIXCU
        EUFUULXSUUQUKXTXKUULXRURVOUUKUULXRURVIXCXDXEXFXGXHXJWE $.
        $( [23-Aug-2007] $)
    $}

    ${
      $d v w A $.  $d v w B $.  $d w D $.  $d w K $.  $d v w P $.  $d v w S $.
      ipasslem7.a $e |- A e. X $.
      ipasslem7.b $e |- B e. X $.
      ipasslem7.d $e |- D = ( abs o. - ) $.
      ipasslem7.c $e |- C = ( ( abs o. - ) |` ( RR X. RR ) ) $.
      ipasslem7.j $e |- J = ( MetOpen ` C ) $.
      ipasslem7.k $e |- K = ( MetOpen ` D ) $.
      ipasslem7.f $e |- F = { <. w , v >. | ( w e. RR /\
              v = ( ( ( w S A ) P B ) - ( w x. ( A P B ) ) ) ) } $.
      $( Lemma for ~ ipassi .  Show that
         ` ( ( w S A ) P B ) - ( w x. ( A P B ) ) ` is continuous on
         ` RR ` . $)
      ipasslem7 $p |- F e. ( J Cn K ) $=
        ( cv cc wcel co cmul cmin wceq wa copab cres ccn wss ax-resscn resopab2
        cr ax-mp eqtr4i cme w3a remet cnmet 3pm3.2i cabs ccom cxp resss 3sstr4i
        cims cfv eqid ipasslem6 pm3.2i remetba metcnss2 mp2an eqeltri ) JAUGZUH
        UIBUGWCCHUJDGUJWCCDGUJUKUJULUJUMZUNABUOZVAUPZLMUQUJZJWCVAUIWDUNABUOZWFU
        FVAUHURWFWHUMUSWDABVAUHUTVBVCEVDUIZFVDUIZWJVEEFURZWEMMUQUJUIZUNWFWGUIWI
        WJWJEUCVFFUBVGZWMVHWKWLVIULVJZVAVAVKZUPWNEFWNWOVLUCUBVMABCDIVNVOZFGHIWE
        KMNOPQRSTUAUBWPVPUEWEVPVQVREFFWELMMVAEUCVSUDUEUEVTWAWB $.
        $( [23-Aug-2007] $)
    $}

    ${
      $d v w A $.  $d v w B $.  $d x F $.  $d v w P $.  $d v w S $.
      $d v w x $.
      ipasslem8.a $e |- A e. X $.
      ipasslem8.b $e |- B e. X $.
      ipasslem8.f $e |- F = { <. w , v >. | ( w e. RR /\
              v = ( ( ( w S A ) P B ) - ( w x. ( A P B ) ) ) ) } $.
      $( Lemma for ~ ipassi .  By ~ ipasslem5 , ` F ` is 0 for all ` QQ ` ;
         since it is continuous and ` QQ ` is dense in ` RR ` by ~ qdensere2 ,
         we conclude ` F ` is 0 for all ` RR ` . $)
      ipasslem8 $p |- F : RR --> { 0 } $=
        ( cr wcel vx cabs cmin ccom cxp cres cme copn cfv ccn co w3a cc cq ccnv
        cc0 csn cima wss ccl wceq wf eqid remet cnmet ipasslem7 3pm3.2i cv wral
        0cn cmul qre opreq1 opreq1d opreq12d oprex fvopab4 syl wa ipasslem5 qcn
        wb cnv phnvi nvscl mp3an1 sylan ipcl mp3an13 mulcl syl2an subeq0 mpbird
        syl11anc mpan2 eqtrd rgen wfun cdm fnopab2 fnfun ax-mp dmopab2 sseqtr4i
        wfn qssre funconstss mp2an mpbi qdensere2 remetba cnmetba metdnsconst )
        UBUCUDZSSUEUFZUGTZXNUGTZHXOUHUIZXNUHUIZUJUKTZULUPUMTZUNHUOUPUQZURUSZUNX
        RUTUIUISVAZULSYBHVBXPXQXTXOXOVCZVDXNXNVCZVEABCDXOXNEFGHIXRXSJKLMNOPQYFY
        EXRVCZXSVCZRVFVGYAYCYDVJUAVHZHUIZUPVAZUAUNVIZYCYKUAUNYIUNTZYJYICFUKZDEU
        KZYICDEUKZVKUKZUCUKZUPYMYISTYJYRVAYIVLABYIAVHZCFUKZDEUKZYSYPVKUKZUCUKZY
        RSHYSYIVAZUUAYOUUBYQUCUUDYTYNDEYSYICFVMVNYSYIYPVKVMVORYOYQUCVPVQVRYMCJT
        ZYRUPVAZPYMUUEVSZUUFYOYQVAZCDYIEFGIJKLMNOQVTUUGYOUMTZYQUMTZUUFUUHWBUUGY
        NJTZUUIYMYIUMTZUUEUUKYIWAZGWCTZUULUUEUUKGOWDZYICFGJKMWEWFWGUUNUUKDJTZUU
        IUUOQYNDEGJKNWHWIVRYMUULYPUMTZUUJUUEUUMUUNUUEUUPUUQUUOQCDEGJKNWHWIYIYPW
        JWKYOYQWLWNWMWOWPWQHWRZUNHWSZUSYLYCWBHSXEUURABSUUCHUUAUUBUCVPZRWTSHXAXB
        UNSUUSXFABSUUCHUUTRXCXDUAUNUPHXGXHXIXOXRYEYGXJVGUNXOXNUPHXRXSSUMXOYEXKX
        NYFXLYGYHXMXH $.
        $( [24-Aug-2007] $)
    $}

    ${
      $d v w A $.  $d v w B $.  $d v w C $.  $d v w P $.  $d v w S $.
      ipasslem9.a $e |- A e. X $.
      ipasslem9.b $e |- B e. X $.
      $( Lemma for ~ ipassi .  Conclude from ~ ipasslem8 the inner product
         associative law for real numbers. $)
      ipasslem9 $p |- ( C e. RR ->
                  ( ( C S A ) P B ) = ( C x. ( A P B ) ) ) $=
        ( cr wcel co cc0 wceq vw vv cmul cmin cv wa cfv opreq1 opreq1d opreq12d
        copab eqid oprex fvopab4 csn wf ipasslem8 fvconst mpan eqtr3d cc wb cnv
        recn phnvi nvscl mp3an13 ipcl mp3an mulcl mpan2 subeq0 syl11anc mpbid
        syl ) CPQZCAERZBDRZCABDRZUCRZUDRZSTZVRVTTZVPCUAUEZPQUBUEWDAERZBDRZWDVSU
        CRZUDRZTUFUAUBUKZUGZWASUAUBCWHWAPWIWDCTZWFVRWGVTUDWKWEVQBDWDCAEUHUIWDCV
        SUCUHUJWIULZVRVTUDUMUNPSUOWIUPVPWJSTUAUBABDEFWIGHIJKLMNOWLUQPSCWIURUSUT
        VPCVAQZWBWCVBZCVDWMVRVAQZVTVAQZWNWMVQHQZWOFVCQZWMAHQZWQFMVEZNCAEFHIKVFV
        GWRWQBHQZWOWTOVQBDFHILVHVGVOWMVSVAQZWPWRWSXAXBWTNOABDFHILVHVICVSVJVKVRV
        TVLVMVOVN $.
        $( [24-Aug-2007] $)
    $}

    ${
      ipasslem10.a $e |- A e. X $.
      ipasslem10.b $e |- B e. X $.
      ipasslem10.6 $e |- N = ( norm ` U ) $.
      $( Lemma for ~ ipassi .  Show the inner product associative law for the
         imaginary number ` _i ` . $)
      ipasslem10 $p |- ( ( _i S A ) P B ) = ( _i x. ( A P B ) ) $=
        ( ci co cmul wcel ccj cfv cneg c4 wceq c2 cexp c1 cmin caddc cnv cc w3a
        phnvi ax-icn 3pm3.2i nvsass mp2an opreq1i eqtr3i fveq2i mulneg1i negeqi
        opreq2i ax-1cn negnegi 3eqtri negcli nvsid 3eqtr3i opreq12i nvgcl mp3an
        nvcli recni sqcli nvscl subcli mulm1i negsubdi2i eqtr2i mulassi mulcomi
        eqtr4i eqtri mulid2i 3eqtr2i mulcli addcomi adddii 3eqtr4i 4ipval2 ipcl
        ixi 4re mul12i 4pos gt0ne0ii mulcani mpbi cjmuli cr 1re renegcli cjrebi
        ipcj cji mul2negi ) BQADRZCRZUAUBZQUCZBACRZSRZUAUBZXIBCRZQABCRZSRZXJXNU
        AUDXJSRZUDXNSRZUEXJXNUEBXIFRZGUBZUFUGRZBUHUCZXIDRZFRZGUBZUFUGRZUIRZQBQX
        IDRZFRZGUBZUFUGRZBXLXIDRZFRZGUBZUFUGRZUIRZSRZUJRZXLUDXMSRZSRZXSXTYTXLBA
        FRZGUBZUFUGRZBYDADRZFRZGUBZUFUGRZUIRZQYCBXLADRZFRZGUBZUFUGRZUIRZSRZUJRZ
        SRZUUBYSYIUJRXLUUJSRZXLUUPSRZUJRYTUURYSUUSYIUUTUJYSQUUIUUEUIRZSRZQYDSRZ
        UUJSRZUUSYRUVAQSYMUUIYQUUEUIYLUUHUFUGYKUUGGYJUUFBFQQSRZADRZYJUUFEUKTZQU
        LTZUVHAHTZUMUVFYJUEEMUNZUVHUVHUVIUOUONUPQQADEHIKUQURUVEYDADWNUSUTVDVAUS
        YPUUDUFUGYOUUCGYNABFXLQSRZADRZUHADRZYNAUVKUHADUVKUVEUCYDUCUHQQUOUOVBUVE
        YDWNVCUHVEVFVGZUSUVGXLULTZUVHUVIUMUVLYNUEUVJUVOUVHUVIQUOVHZUONUPXLQADEH
        IKUQURUVGUVIUVMAUEUVJNADEHIKVIURVJVDVAUSVKVDUVBQYDUUJSRZSRUVDUVAUVQQSUV
        QUUJUCUVAUUJUUEUUIUUDUUDUUCEGHIPUVJUVGBHTZUVIUUCHTUVJONBAEFHIJVLVMVNVOV
        PZUUHUUHUUGEGHIPUVJUVGUVRUUFHTZUUGHTUVJOUVGYDULTZUVIUVTUVJUHVEVHZNYDADE
        HIKVQVMBUUFEFHIJVLVMVNVOVPZVRZVSUUEUUIUVSUWCVTWAVDQYDUUJUOUWBUWDWBWDUVC
        XLUUJSUVCYDQSRZXLQYDUOUWBWCQUOVSZWEUSVGYIUHUUOSRZUVKUUOSRUUTYIUUOUWGYHU
        UNYCUIYGUUMUFUGYFUULGYEUUKBFUWEADRZYEUUKUVGUWAUVHUVIUMUWHYEUEUVJUWAUVHU
        VIUWBUONUPYDQADEHIKUQURUWEXLADUWFUSUTVDVAUSVDUUOYCUUNYBYBYAEGHIPUVJUVGU
        VRXIHTZYAHTUVJOUVGUVHUVIUWIUVJUONQADEHIKVQVMZBXIEFHIJVLVMVNVOVPZUUMUUMU
        ULEGHIPUVJUVGUVRUUKHTZUULHTUVJOUVGUVOUVIUWLUVJUVPNXLADEHIKVQVMBUUKEFHIJ
        VLVMVNVOVPVRZWFWDUVKUHUUOSUVNUSXLQUUOUVPUOUWMWBWGVKYIYSYCYHUWKYGYGYFEGH
        IPUVJUVGUVRYEHTZYFHTUVJOUVGUWAUWIUWNUVJUWBUWJYDXIDEHIKVQVMBYEEFHIJVLVMV
        NVOVPVRQYRUOYMYQYLYLYKEGHIPUVJUVGUVRYJHTZYKHTUVJOUVGUVHUWIUWOUVJUOUWJQX
        IDEHIKVQVMBYJEFHIJVLVMVNVOVPYPYPYOEGHIPUVJUVGUVRYNHTZYOHTUVJOUVGUVOUWIU
        WPUVJUVPUWJXLXIDEHIKVQVMBYNEFHIJVLVMVNVOVPVRWHWIXLUUJUUPUVPUWDQUUOUOUWM
        WHWJWKUUAUUQXLSUVGUVRUVIUUAUUQUEUVJONBACDEFGHIJKPLWLVMVDWDUVGUVRUWIXSYT
        UEUVJOUWJBXICDEFGHIJKPLWLVMUDXLXMUDWOVOZUVPUVGUVRUVIXMULTUVJONBACEHILWM
        VMZWPWKXJXNUDUVGUVRUWIXJULTUVJOUWJBXICEHILWMVMXLXMUVPUWRWHUWQUDWOWQWRWS
        WTVAUVGUVRUWIXKXPUEUVJOUWJBXICEHILXFVMXOXLUAUBZXMUAUBZSRXRXLXMUVPUWRXAU
        WSQUWTXQSUWSYDUAUBZQUAUBZSRZYDXLSRZQUWEUAUBUWSUXCUWEXLUAUWFVAYDQUWBUOXA
        UTUXAYDUXBXLSYDXBTUXAYDUEUHXCXDYDUWBXEWTXGVKUXDUHQSRQUHQVEUOXHQUOWFWEVG
        UVGUVRUVIUWTXQUEUVJONBACEHILXFVMVKWEVJ $.
        $( [24-Aug-2007] $)
    $}

    ${
      $d x B $.  $d x y C $.  $d x y P $.
      ipasslem11.a $e |- A e. X $.
      ipasslem11.b $e |- B e. X $.
      $( MAY BE REVISED - proof got longer with axcnreOLD -> ax-cnre because
         of ax-mulcl - is it possible to rewrite it to shorten it? $)
      $( Lemma for ~ ipassi .  Show the inner product associative law for all
         complex numbers. $)
      ipasslem11 $p |- ( C e. CC ->
                  ( ( C S A ) P B ) = ( C x. ( A P B ) ) ) $=
        ( wcel ci cmul co wceq vx vy cc cv caddc cr wrex ax-cnre wa ax-icn recn
        mulcom sylancr adantl opreq2d eqeq2d mulcl sylancl cnv phnvi nvdir mpan
        w3a mp3an3 syl2an opreq1d nvscl mp3an13 ipdiri ipasslem9 nvsass mp3an23
        syl mp3an ipcl mulass cnm ipasslem10 opreq2i syl6eqr 3eqtr4d opreqan12d
        eqid 3eqtrd adddir eqtr4d opreq1 eqeq12d syl5ibrcom sylbid r19.23aivv
        cfv ) CUCPCUAUDZQUBUDZRSZUESZTZUBUFUGUAUFUGCAESZBDSZCABDSZRSZTZUAUBCUHW
        QXBUAUBUFUFWMUFPZWNUFPZUIZWQCWMWNQRSZUESZTZXBXEWPXGCXEWOXFWMUEXDWOXFTZX
        CXDQUCPZWNUCPZXIUJWNUKZQWNULUMUNUOUPXEXBXHXGAESZBDSZXGWTRSZTXEXNWMWTRSZ
        XFWTRSZUESZXOXEXNWMAESZXFAESZGSZBDSZXSBDSZXTBDSZUESZXRXEXMYABDXCWMUCPZX
        FUCPZXMYATZXDWMUKZXDXKXJYGXLUJWNQUQURZYFYGAHPZYHNFUSPZYFYGYKVCYHFMUTZWM
        XFAEFGHIJKVAVBVDVEVFXCXSHPZXTHPZYBYETZXDXCYFYNYIYLYFYKYNYMNWMAEFHIKVGVH
        VMXDYGYOYJYLYGYKYOYMNXFAEFHIKVGVHVMYNYOBHPZYPOXSXTBDEFGHIJKLMVIVDVEXCXD
        YCXPYDXQUEABWMDEFGHIJKLMNOVJXDWNQAESZESZBDSWNYRBDSZRSZYDXQYRBWNDEFGHIJK
        LMYLXJYKYRHPYMUJNQAEFHIKVGVNOVJXDXTYSBDXDXKXTYSTZXLXKXJYKUUBUJNYLXKXJYK
        VCUUBYMWNQAEFHIKVKVBVLVMVFXDXQWNQWTRSZRSZUUAXDXKXQUUDTZXLXKXJWTUCPZUUEU
        JYLYKYQUUFYMNOABDFHILVOVNZWNQWTVPVLVMYTUUCWNRABDEFGFVQWLZHIJKLMNOUUHWCV
        RVSVTWAWBWDXCYFYGXOXRTZXDYIYJYFYGUUFUUIUUGWMXFWTWEVDVEWFXHWSXNXAXOXHWRX
        MBDCXGAEWGVFCXGWTRWGWHWIWJWKVM $.
        $( [25-Aug-2007] $)
    $}

    $( Associative law for inner product.  Equation I2 of [Ponnusamy]
       p. 363. $)
    ipassi $p |- ( ( A e. CC /\ B e. X /\ C e. X ) ->
                  ( ( A S B ) P C ) = ( A x. ( B P C ) ) ) $=
      ( wcel co cmul wceq wi cif opreq2 cn0v cfv opreq1d opreq1 opreq2d eqeq12d
      cc wa imbi2d eqid elimph ipasslem11 dedth2h com12 3impib ) AUGNZBHNZCHNZA
      BEOZCDOZABCDOZPOZQZUQURUHUPVCUQURUPVCRUPAUQBFUAUBZSZEOZCDOZAVECDOZPOZQZRU
      PVFURCVDSZDOZAVEVKDOZPOZQZRBCVDVDBVEQZVCVJUPVPUTVGVBVIVPUSVFCDBVEAETUCVPV
      AVHAPBVECDUDUEUFUICVKQZVJVOUPVQVGVLVIVNCVKVFDTVQVHVMAPCVKVEDTUEUFUIVEVKAD
      EFGHIJKLMBFHVDIVDUJZMUKCFHVDIVRMUKULUMUNUO $.
      $( [25-Aug-2007] $)
  $}

  ${
    ipdir.1 $e |- X = ( BaseSet ` U ) $.
    ipdir.2 $e |- G = ( +v ` U ) $.
    ipdir.7 $e |- P = ( .i ` U ) $.
    $( Distributive law for inner product.  Equation I3 of [Ponnusamy]
       p. 362. $)
    ipdir $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) P C ) = ( ( A P C ) + ( B P C ) ) ) $=
      ( wcel co caddc wceq cba cfv cpv cip opreqd eqid cphl w3a wi cmul cop cif
      cabs fveq2 syl5eq eleq2d 3anbi123d opreq1d eqtrd opreq12d eqeq12d imbi12d
      cns elimphu ipdiri dedth imp ) EUAKZAGKZBGKZCGKZUBZABFLZCDLZACDLZBCDLZMLZ
      NZVBVFVLUCAVBEMUDUEUGUEZUFZOPZKZBVOKZCVOKZUBZABVNQPZLZCVNRPZLZACWBLZBCWBL
      ZMLZNZUCEVMEVNNZVFVSVLWGWHVCVPVDVQVEVRWHGVOAWHGEOPVOHEVNOUHUIZUJWHGVOBWIU
      JWHGVOCWIUJUKWHVHWCVKWFWHVHWACDLWCWHVGWACDWHFVTABWHFEQPVTIEVNQUHUISULWHDW
      BWACWHDERPWBJEVNRUHUIZSUMWHVIWDVJWEMWHDWBACWJSWHDWBBCWJSUNUOUPABCWBVNUQPZ
      VNVTVOVOTVTTWKTWBTEURUSUTVA $.
      $( [25-Aug-2007] $)

    $( Distributive law for inner product. $)
    ipdi $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( A P ( B G C ) ) = ( ( A P B ) + ( A P C ) ) ) $=
      ( wcel w3a co caddc wceq id wa ccj cfv ipcj cphl 3com13 3com12 sylan2 cnv
      ipdir fveq2d simpl nvgcl 3com23 3adant3r3 simp3 adantl syl111anc sylan cc
      phnv ipcl 3adant3r1 3adant3r2 cjadd syl11anc opreq12d eqtrd 3eqtr3d ) AGK
      ZBGKZCGKZLEUAKZVHVGVFLZABCFMZDMZABDMZACDMZNMZOVHVGVFVJVJPUBVIVJQZVKADMZRS
      ZBADMZCADMZNMZRSZVLVOVPVQWARVJVIVGVHVFLZVQWAOVGVHVFWCWCPUCBCADEFGHIJUFUDU
      GVIEUEKZVJVRVLOZEUQZWDVJQZWDVKGKZVFWEWDVJUHWDVHVGWHVFWDVGVHWHBCEFGHIUIUJU
      KVJVFWDVHVGVFULUMVKADEGHJTUNUOVIWDVJWBVOOWFWGWBVSRSZVTRSZNMZVOWGVSUPKZVTU
      PKZWBWKOWDVGVFWLVHBADEGHJURUSWDVHVFWMVGCADEGHJURUTVSVTVAVBWGWIVMWJVNNWDVG
      VFWIVMOVHBADEGHJTUSWDVHVFWJVNOVGCADEGHJTUTVCVDUOVEUD $.
      $( [20-Nov-2007] $)
  $}

  ${
    ip2dii.1 $e |- X = ( BaseSet ` U ) $.
    ip2dii.2 $e |- G = ( +v ` U ) $.
    ip2dii.7 $e |- P = ( .i ` U ) $.
    ip2dii.u $e |- U e. CPreHil $.
    ip2dii.a $e |- A e. X $.
    ip2dii.b $e |- B e. X $.
    ip2dii.c $e |- C e. X $.
    ip2dii.d $e |- D e. X $.
    $( Inner product of two sums. $)
    ip2dii $p |- ( ( A G B ) P ( C G D ) )
                = ( ( ( A P C ) + ( B P D ) ) + ( ( A P D ) + ( B P C ) ) ) $=
      ( co caddc wcel mp3an cphl w3a wceq 3pm3.2i ipdi mp2an opreq12i cnv phnvi
      nvgcl ipdir cc ipcl add42i 3eqtr4i ) ACDGQZEQZBUPEQZRQZACEQZADEQZRQZBCEQZ
      BDEQZRQZRQABGQUPEQZUTVDRQVAVCRQRQUQVBURVERFUASZAHSZCHSZDHSZUBUQVBUCLVHVIV
      JMOPUDACDEFGHIJKUEUFVGBHSZVIVJUBURVEUCLVKVIVJNOPUDBCDEFGHIJKUEUFUGVGVHVKU
      PHSZUBVFUSUCLVHVKVLMNFUHSZVIVJVLFLUIZOPCDFGHIJUJTUDABUPEFGHIJKUKUFUTVDVAV
      CVMVHVIUTULSVNMOACEFHIKUMTVMVKVJVDULSVNNPBDEFHIKUMTVMVHVJVAULSVNMPADEFHIK
      UMTVMVKVIVCULSVNNOBCEFHIKUMTUNUO $.
      $( [17-Apr-2008] $)
  $}

  ${
    ipass.1 $e |- X = ( BaseSet ` U ) $.
    ipass.4 $e |- S = ( .s ` U ) $.
    ipass.7 $e |- P = ( .i ` U ) $.
    $( Associative law for inner product.  Equation I2 of [Ponnusamy]
       p. 363. $)
    ipass $p |- ( ( U e. CPreHil /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
                 ( ( A S B ) P C ) = ( A x. ( B P C ) ) ) $=
      ( wcel co cmul wceq cba cfv cns cip fveq2 eqid cphl cc w3a caddc cop cabs
      cif syl5eq eleq2d 3anbi23d opreqd opreq1d opreq2d eqeq12d imbi12d elimphu
      wi eqtrd cpv ipassi dedth imp ) FUAKZAUBKZBGKZCGKZUCZABELZCDLZABCDLZMLZNZ
      VCVGVLUQVDBVCFUDMUEUFUEZUGZOPZKZCVOKZUCZABVNQPZLZCVNRPZLZABCWALZMLZNZUQFV
      MFVNNZVGVRVLWEWFVEVPVFVQVDWFGVOBWFGFOPVOHFVNOSUHZUIWFGVOCWGUIUJWFVIWBVKWD
      WFVIVTCDLWBWFVHVTCDWFEVSABWFEFQPVSIFVNQSUHUKULWFDWAVTCWFDFRPWAJFVNRSUHZUK
      URWFVJWCAMWFDWABCWHUKUMUNUOABCWAVSVNVNUSPZVOVOTWITVSTWATFUPUTVAVB $.
      $( [25-Aug-2007] $)

    $( "Associative" law for second argument of inner product (compare
       ~ ipass ). $)
    ipassr $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. CC /\ C e. X ) ) ->
                 ( A P ( B S C ) ) = ( ( * ` B ) x. ( A P C ) ) ) $=
      ( wcel cc w3a wa co ccj cfv cmul wceq adantl cphl 3anrot ipass fveq2d cnv
      sylan2b phnv simpl nvscl 3adant3r1 simp1 ipcj syl111anc sylan ipcl 3com23
      simp2 3adant3r2 cjmul syl11anc opreq2d eqtrd 3eqtr3d ) FUAKZAGKZBLKZCGKZM
      ZNZBCEOZADOZPQZBCADOZROZPQZAVJDOZBPQZACDOZROZVIVKVNPVHVDVFVGVEMVKVNSVEVFV
      GUBBCADEFGHIJUCUFUDVDFUEKZVHVLVPSZFUGZVTVHNZVTVJGKZVEWAVTVHUHVTVFVGWDVEBC
      EFGHIUIUJVHVEVTVEVFVGUKTVJADFGHJULUMUNVDVTVHVOVSSWBWCVOVQVMPQZROZVSWCVFVM
      LKZVOWFSVHVFVTVEVFVGUQTVTVEVGWGVFVTVGVEWGCADFGHJUOUPURBVMUSUTWCWEVRVQRVTV
      EVGWEVRSZVFVTVGVEWHCADFGHJULUPURVAVBUNVC $.
      $( [22-Nov-2007] $)

    $( "Associative" law for inner product.  Conjugate version of ~ ipassr . $)
    ipassr2 $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. CC /\ C e. X ) ) ->
                 ( A P ( ( * ` B ) S C ) ) = ( B x. ( A P C ) ) ) $=
      ( cphl wcel cc w3a wa ccj cfv co cmul wceq cjcl syl3anr2 3ad2ant2 opreq1d
      ipassr cjcj adantl eqtrd ) FKLZAGLZBMLZCGLZNZOZABPQZCERDRZUOPQZACDRZSRZBU
      RSRUKUJUIUOMLULUPUSTBUAAUOCDEFGHIJUEUBUNUQBURSUMUQBTZUIUKUJUTULBUFUCUGUDU
      H $.
      $( [23-Nov-2007] $)
  $}

  ${
    ipsubdir.1 $e |- X = ( BaseSet ` U ) $.
    ipsubdir.3 $e |- M = ( -v ` U ) $.
    ipsubdir.7 $e |- P = ( .i ` U ) $.
    $( Distributive law for inner product subtraction. $)
    ipsubdir $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A M B ) P C ) = ( ( A P C ) - ( B P C ) ) ) $=
      ( wcel w3a wa c1 cneg co caddc wceq cc sylan cphl cns cfv cpv cmin ax-1cn
      idd cnv phnv negcli eqid nvscl mp3an2 3anim123d imp syldan nvmval syl3an1
      ipdir 3adant3r3 opreq1d cmul ipass mp3anr1 3expb mulm1 syl eqtrd 3adantr1
      ex ipcl opreq2d 3adant3r2 3adant3r1 negsub syl11anc eqtr2d 3eqtr4d ) EUAK
      ZAGKZBGKZCGKZLZMZANOZBEUBUCZPZEUDUCZPZCDPZACDPZWGCDPZQPZABFPZCDPWKBCDPZUE
      PZVSWCVTWGGKZWBLZWJWMRVSWCWRVSVTVTWAWQWBWBVSVTUGVSWAWQVSEUHKZWAWQEUIZWSWE
      SKZWAWQNUFUJZWEBWFEGHWFUKZULUMTVJVSWBUGUNUOAWGCDEWHGHWHUKZJUSUPWDWNWICDVS
      VTWAWNWIRZWBVSWSVTWAXEWTABWFEWHFGHXDXCIUQURUTVAWDWMWKWOOZQPZWPWDWLXFWKQVS
      WAWBWLXFRVTVSWAWBMZMZWLWEWOVBPZXFVSXAWAWBWLXJRXBWEBCDWFEGHXCJVCVDXIWOSKZX
      JXFRVSWSXHXKWTWSWAWBXKBCDEGHJVKZVETWOVFVGVHVIVLVSWSWCXGWPRZWTWSWCMWKSKZXK
      XMWSVTWBXNWAACDEGHJVKVMWSWAWBXKVTXLVNWKWOVOVPTVQVR $.
      $( [20-Nov-2007] $)

    $( Distributive law for inner product subtraction. $)
    ipsubdi $p |- ( ( U e. CPreHil /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( A P ( B M C ) ) = ( ( A P B ) - ( A P C ) ) ) $=
      ( wcel w3a co cmin wceq id wa ccj cfv ipcj cphl 3com13 3com12 sylan2 phnv
      ipsubdir fveq2d simpl nvmcl 3com23 3adant3r3 simp3 adantl syl111anc sylan
      cnv cc ipcl 3adant3r1 3adant3r2 cjsub syl11anc opreq12d eqtrd 3eqtr3d )
      AGKZBGKZCGKZLEUAKZVHVGVFLZABCFMZDMZABDMZACDMZNMZOVHVGVFVJVJPUBVIVJQZVKADM
      ZRSZBADMZCADMZNMZRSZVLVOVPVQWARVJVIVGVHVFLZVQWAOVGVHVFWCWCPUCBCADEFGHIJUF
      UDUGVIEUPKZVJVRVLOZEUEZWDVJQZWDVKGKZVFWEWDVJUHWDVHVGWHVFWDVGVHWHBCEFGHIUI
      UJUKVJVFWDVHVGVFULUMVKADEGHJTUNUOVIWDVJWBVOOWFWGWBVSRSZVTRSZNMZVOWGVSUQKZ
      VTUQKZWBWKOWDVGVFWLVHBADEGHJURUSWDVHVFWMVGCADEGHJURUTVSVTVAVBWGWIVMWJVNNW
      DVGVFWIVMOVHBADEGHJTUSWDVHVFWJVNOVGCADEGHJTUTVCVDUOVEUD $.
      $( [20-Nov-2007] $)
  $}

  ${
    pyth.1 $e |- X = ( BaseSet ` U ) $.
    pyth.2 $e |- G = ( +v ` U ) $.
    pyth.6 $e |- N = ( norm ` U ) $.
    pyth.7 $e |- P = ( .i ` U ) $.
    pythi.u $e |- U e. CPreHil $.
    pythi.a $e |- A e. X $.
    pythi.b $e |- B e. X $.
    $( The Pythagorean theorem for an arbitrary complex inner product
       (pre-Hilbert) space ` U ` .  The square of the norm of the sum of two
       orthogonal vectors (i.e. whose inner product is 0) is the sum of the
       squares of their norms.  Problem 2 in [Kreyszig] p. 135. $)
    pythi $p |- ( ( A P B ) = 0 -> ( ( N ` ( A G B ) ) ^ 2 )
        = ( ( ( N ` A ) ^ 2 ) + ( ( N ` B ) ^ 2 ) ) ) $=
      ( co cc0 wceq caddc wcel mp3an cfv c2 cexp ip2dii id cnv iporthcom biimpi
      wb phnvi opreq12d 0cn addid1i syl6eq opreq2d cc ipcl addcli syl5eq ipidsq
      nvgcl mp2an opreq12i 3eqtr3g ) ABCOZPQZABEOZVGCOZAACOZBBCOZROZVGFUAUBUCOZ
      AFUAUBUCOZBFUAUBUCOZROVFVHVKVEBACOZROZROZVKABABCDEGHIKLMNMNUDVFVQVKPROVKV
      FVPPVKRVFVPPPROPVFVEPVOPRVFUEVFVOPQZDUFSZAGSZBGSZVFVRUIDLUJZMNABCDGHKUGTU
      HUKPULUMUNUOVKVIVJVSVTVTVIUPSWBMMAACDGHKUQTVSWAWAVJUPSWBNNBBCDGHKUQTURUMU
      NUSVSVGGSZVHVLQWBVSVTWAWCWBMNABDEGHIVATVGCDFGHJKUTVBVIVMVJVNRVSVTVIVMQWBM
      ACDFGHJKUTVBVSWAVJVNQWBNBCDFGHJKUTVBVCVD $.
      $( [17-Apr-2008] $)
  $}

  ${
    siii.1 $e |- X = ( BaseSet ` U ) $.
    siii.6 $e |- N = ( norm ` U ) $.
    siii.7 $e |- P = ( .i ` U ) $.
    siii.9 $e |- U e. CPreHil $.
    siii.a $e |- A e. X $.
    siii.b $e |- B e. X $.
    ${
      sii1.3 $e |- M = ( -v ` U ) $.
      sii1.4 $e |- S = ( .s ` U ) $.
      sii1.c $e |- C e. CC $.
      sii1.r $e |- ( C x. ( A P B ) ) e. RR $.
      sii1.z $e |- 0 <_ ( C x. ( A P B ) ) $.
      $( Lemma for ~ sii . $)
      siilem1 $p |- ( ( B P A ) = ( C x. ( ( N ` B ) ^ 2 ) ) ->
                ( sqr ` ( ( A P B ) x. ( C x. ( ( N ` B ) ^ 2 ) ) ) ) <_
                       ( ( N ` A ) x. ( N ` B ) ) ) $=
        ( co cfv c2 cexp cmul wceq csqr cle wbr cc0 cmin ccj phnvi cnv cc cjcli
        wcel nvscl mp3an nvmcl nvcli sqge0i cphl w3a 3pm3.2i mp2an ipidsq ipass
        ipsubdi ipassr2 opreq2i eqtri recni sqcli ipcl mulcli ipsubdir opreq12i
        sub4 mp4an opreq1i subdii 3eqtr4i 3eqtr3i breqtri subeq0i opreq2 mul01i
        syl6eq sylbir opreq2d resqcli subcli subid1i syl5breq subge0i cr pm3.2i
        sylib lemul1a mpan syl sqmuli syl6breqr mulge0i remulcli sqrlei mulcomi
        wa wb mulassi fveq2i nvge0 sqrsqi ax-mp 3brtr3g ) BADUAZCBHUBZUCUDUAZUE
        UAZUFZCABDUAZUEUAZXSUEUAZUGUBZAHUBZXRUEUAZUCUDUAZUGUBZYBXTUEUAZUGUBYGUH
        YAYDYHUHUIZYEYIUHUIZYAYDYFUCUDUAZXSUEUAZYHUHYAYCYMUHUIZYDYNUHUIZYAUJYMY
        CUKUAZUHUIYOYAUJYQCULUBZXQXTUKUAZUEUAZUKUAZYQUHUJAYRBEUAZGUAZHUBZUCUDUA
        ZUUAUHUUDUUCFHIJKFMUMZFUNUQZAIUQZUUBIUQZUUCIUQZUUFNUUGYRUOUQZBIUQZUUIUU
        FCRUPZOYRBEFIJQURUSZAUUBFGIJPUTUSZVAVBUUCUUCDUAZUUCADUAZUUCUUBDUAZUKUAZ
        UUEUUAFVCUQZUUJUUHUUIVDUUPUUSUFMUUJUUHUUIUUONUUNVEUUCAUUBDFGIJPLVIVFUUG
        UUJUUPUUEUFUUFUUOUUCDFHIJKLVGVFYMYRXQUEUAZUKUAZYCUUBUUBDUAZUKUAZUKUAZYQ
        UVAYRXTUEUAZUKUAZUKUAZUUSUUAUVEUVBYCUVFUKUAZUKUAZUVHUVDUVIUVBUKUVCUVFYC
        UKUVCYRBUUBDUAZUEUAZUVFUUTUUKUULUUIVDUVCUVLUFMUUKUULUUIUUMOUUNVEYRBUUBD
        EFIJQLVHVFUVKXTYRUEUVKCBBDUAZUEUAZXTUUTUULCUOUQZUULVDUVKUVNUFMUULUVOUUL
        OROVEBCBDEFIJQLVJVFUVMXSCUEUUGUULUVMXSUFUUFOBDFHIJKLVGVFVKVLVKVLVKVKYMU
        OUQUVAUOUQYCUOUQUVFUOUQUVJUVHUFYFYFAFHIJKUUFNVAZVMZVNYRXQUUMUUGUULUUHXQ
        UOUQUUFONBADFIJLVOUSZVPYCSVMZYRXTUUMCXSRXRXRBFHIJKUUFOVAZVMZVNVPZVPYMUV
        AYCUVFVSVTVLUUQUVBUURUVDUKUUQAADUAZUUBADUAZUKUAZUVBUUTUUHUUIUUHVDUUQUWE
        UFMUUHUUIUUHNUUNNVEAUUBADFGIJPLVQVFUWCYMUWDUVAUKUUGUUHUWCYMUFUUFNADFHIJ
        KLVGVFUUTUUKUULUUHVDUWDUVAUFMUUKUULUUHUUMONVEYRBADEFIJQLVHVFVRVLUURAUUB
        DUAZUVCUKUAZUVDUUTUUHUUIUUIVDUURUWGUFMUUHUUIUUINUUNUUNVEAUUBUUBDFGIJPLV
        QVFUWFYCUVCUKUUTUUHUVOUULVDUWFYCUFMUUHUVOUULNROVEACBDEFIJQLVJVFWAVLVRYT
        UVGYQUKYRXQXTUUMUVRUWBWBVKWCWDWEYAUUAYQUJUKUAYQYAYTUJYQUKYAYSUJUFZYTUJU
        FXQXTUVRUWBWFUWHYTYRUJUEUAUJYSUJYRUEWGYRUUMWHWIWJWKYQYMYCYMYFUVPWLZVMUV
        SWMWNWIWOYMYCUWISWPWSYCWQUQZYMWQUQZXSWQUQZUJXSUHUIZXIZVDYOYPUWJUWKUWNSU
        WIUWLUWMXRUVTWLZXRUVTVBZWRVEYCYMXSWTXAXBYFXRUVQUWAXCXDUJYDUHUIZUJYHUHUI
        YKYLXJUJYCUHUIUWMUWQTUWPYCXSSUWOXEVFYGYFXRUVPUVTXFZVBYDYHYCXSSUWOXFYGUW
        RWLXGVFWSYDYJUGYDYBCUEUAZXSUEUAYJYCUWSXSUECYBRUUGUUHUULYBUOUQUUFNOABDFI
        JLVOUSZXHWAYBCXSUWTRXSUWOVMXKVLXLUJYGUHUIZYIYGUFUJYFUHUIZUJXRUHUIZUXAUU
        GUUHUXBUUFNAFHIJKXMVFUUGUULUXCUUFOBFHIJKXMVFYFXRUVPUVTXEVFYGUWRXNXOXP
        $.
        $( [23-Nov-2007] $)
    $}

    ${
      siii2.3 $e |- M = ( -v ` U ) $.
      siii2.4 $e |- S = ( .s ` U ) $.
      $( Lemma for ~ sii . $)
      siilem2 $p |- ( ( C e. CC /\ ( C x. ( A P B ) ) e. RR /\
                           0 <_ ( C x. ( A P B ) ) ) ->
                ( ( B P A ) = ( C x. ( ( N ` B ) ^ 2 ) ) ->
                  ( sqr ` ( ( A P B ) x. ( C x. ( ( N ` B ) ^ 2 ) ) ) ) <_
                         ( ( N ` A ) x. ( N ` B ) ) ) ) $=
        ( co cmul cc0 cc wcel cr cle wbr w3a c2 cexp wceq csqr wi opreq1 eqeq2d
        cfv cif opreq2d fveq2d breq1d imbi12d eleq1 eleq1d breq2d 3anbi123d 0cn
        cnv phnvi ipcl mp3an mul02i 0re eqeltri breqtrri 3pm3.2i elimhyp simp1i
        leidi simp2i simp3i siilem1 dedth ) CUAUBZCABDRZSRZUCUBZTWCUDUEZUFZBADR
        ZCBHUNZUGUHRZSRZUIZWBWJSRZUJUNZAHUNWHSRZUDUEZUKWGWFCTUOZWISRZUIZWBWQSRZ
        UJUNZWNUDUEZUKCTCWPUIZWKWRWOXAXBWJWQWGCWPWISULZUMXBWMWTWNUDXBWLWSUJXBWJ
        WQWBSXCUPUQURUSABWPDEFGHIJKLMNOPQWPUAUBZWPWBSRZUCUBZTXEUDUEZWFXDXFXGUFT
        UAUBZTWBSRZUCUBZTXIUDUEZUFCTXBWAXDWDXFWEXGCWPUAUTXBWCXEUCCWPWBSULZVAXBW
        CXETUDXLVBVCTWPUIZXHXDXJXFXKXGTWPUAUTXMXIXEUCTWPWBSULZVAXMXIXETUDXNVBVC
        XHXJXKVDXITUCWBFVEUBAIUBBIUBWBUAUBFMVFNOABDFIJLVGVHVIZVJVKTTXIUDTVJVPXO
        VLVMVNZVOXDXFXGXPVQXDXFXGXPVRVSVT $.
        $( [24-Nov-2007] $)
    $}

    $( Inference from ~ sii . $)
    siii $p |- ( abs ` ( A P B ) ) <_ ( ( N ` A ) x. ( N ` B ) ) $=
      ( co cfv cmul cle wbr wceq cc0 wcel cabs cn0v opreq2 cnv phnvi eqid mp2an
      ip0r syl6eq fveq2d abs0 nvge0 nvcli mulge0i syl6eqbr wne c2 cexp cdiv ccj
      csqr recni sqeq0i wb nvz bitri necon3bii cc ipcl resqcli divcan1zi sylbir
      mp3an ipcj syl6eqr opreq2d absval ax-mp syl6reqr eqcomd cr divclzi ipipcj
      wi mulcomi eqtri opreq1i wa div23 mp3an12 mpan syl5reqr redivclzi eqeltrd
      abscli clt sqgt0i sqge0i divge0 mpanl12 syl breqtrrd cns cnsb siilem2 mpd
      syl111anc eqbrtrd pm2.61ine ) ABCMZUANZAENZBENZOMZPQBDUBNZBXORZXKSXNPXPXK
      SUANSXPXJSUAXPXJAXOCMZSBXOACUCDUDTZAFTZXQSRDJUEZKACDFXOGXOUFZIUHUGUIUJUKU
      ISXLPQZSXMPQZSXNPQXRXSYBXTKADEFGHULUGXRBFTZYCXTLBDEFGHULUGXLXMADEFGHXTKUM
      BDEFGHXTLUMZUNUGUOBXOUPZXKXJBACMZXMUQURMZUSMZYHOMZOMZVANZXNPYFYLXJXJUTNZO
      MZVANZXKYFYKYNVAYFYJYMXJOYFYJYGYMYFYHSUPZYJYGRYHSBXOYHSRXMSRZXPXMXMYEVBVC
      XRYDYQXPVDXTLBDEFXOGYAHVEUGZVFVGZYGYHXRYDXSYGVHTZXTLKBACDFGIVIVMZYHXMYEVJ
      ZVBZVKVLZXRXSYDYMYGRXTKLABCDFGIVNVMVOVPUJXJVHTZXKYORXRXSYDUUEXTKLABCDFGIV
      IVMZXJVQVRVSYFYGYJRZYLXNPQZYFYJYGUUDVTYFYIVHTZYIXJOMZWATSUUJPQUUGUUHWDYFY
      PUUIYSYGYHUUAUUCWBVLYFUUJXKUQURMZYHUSMZWAYFUULYGXJOMZYHUSMZUUJUUMUUKYHUSU
      UMXJYGOMZUUKYGXJUUAUUFWEXRXSYDUUOUUKRXTKLABCDFGIWCVMWFWGYFYPUUNUUJRZYSYHV
      HTZYPUUPUUCYTUUEUUQYPWHUUPUUAUUFYGXJYHWIWJWKVLWLZYFYPUULWATYSUUKYHXKXJUUF
      WOZVJZUUBWMVLWNYFSUULUUJPYFSYHWPQZSUULPQZYFXMSUPUVAXMSBXOYRVGXMYEWQVLYHWA
      TZUVAUVBUUBUUKWATSUUKPQUVCUVAWHUVBUUTXKUUSWRUUKYHWSWTWKXAUURXBABYICDXCNZD
      DXDNZEFGHIJKLUVEUFUVDUFXEXGXFXHXI $.
      $( [20-Nov-2007] $)
  $}

  ${
    sii.1 $e |- X = ( BaseSet ` U ) $.
    sii.6 $e |- N = ( norm ` U ) $.
    sii.7 $e |- P = ( .i ` U ) $.
    sii.9 $e |- U e. CPreHil $.
    $( Schwartz inequality.  Part of Lemma 3-2.1(a) of [Kreyszig] p. 137. $)
    sii $p |- ( ( A e. X /\ B e. X ) ->
               ( abs ` ( A P B ) ) <_ ( ( N ` A ) x. ( N ` B ) ) ) $=
      ( wcel co cabs cfv cmul cle wbr cif wceq fveq2d cn0v opreq1 fveq2 opreq1d
      breq12d opreq2 opreq2d eqid elimph siii dedth2h ) AFKZBFKZABCLZMNZAENZBEN
      ZOLZPQULADUANZRZBCLZMNZUTENZUQOLZPQUTUMBUSRZCLZMNZVCVEENZOLZPQABUSUSAUTSZ
      UOVBURVDPVJUNVAMAUTBCUBTVJUPVCUQOAUTEUCUDUEBVESZVBVGVDVIPVKVAVFMBVEUTCUFT
      VKUQVHVCOBVEEUCUGUEUTVECDEFGHIJADFUSGUSUHZJUIBDFUSGVLJUIUJUK $.
      $( [12-Jan-2008] $)
  $}

  ${
    $d x y H $.  $d x y U $.  $d x y W $.
    sspph.h $e |- H = ( SubSp ` U ) $.
    $( A subspace of an inner product space is an inner product space. $)
    sspph $p |- ( ( U e. CPreHil /\ W e. H ) -> W e. CPreHil ) $=
      ( vx vy wcel wa cfv co c2 cexp caddc cmul wceq sylan eqid sspnval opreq1d
      3expa cphl cnv cv cpv cnm cnsb cba wral phnv sspnv wi sspba sseld anim12d
      imp phpar2 3expb adantlr syldan nvgcl sspgval fveq2d eqtrd nvmcl opreq12d
      sspmval sylanl1 adantrr adantrl opreq2d 3eqtr4d r19.21aivv isph sylanbrc
      ex ) AUAGZCBGZHZCUBGZEUCZFUCZCUDIZJZCUEIZIZKLJZVTWACUFIZJZWDIZKLJZMJZKVTW
      DIZKLJZWAWDIZKLJZMJZNJZOZFCUGIZUHEWSUHCUAGVPAUBGZVQVSAUIZABCDUJZPVRWREFWS
      WSVRVTWSGZWAWSGZHZWRVRXEHZVTWAAUDIZJZAUEIZIZKLJZVTWAAUFIZJZXIIZKLJZMJZKVT
      XIIZKLJZWAXIIZKLJZMJZNJZWKWQVRXEVTAUGIZGZWAYCGZHZXPYBOZVRXEYFVPWTVQXEYFUK
      XAWTVQHZXCYDXDYEYHWSYCVTABCYCWSYCQZWSQZDULZUMYHWSYCWAYKUMUNPUOVPYFYGVQVPY
      DYEYGVTWAAXGXLXIYCYIXGQZXLQZXIQZUPUQURUSVPWTVQXEWKXPOXAYHXEHZWFXKWJXOMYOW
      FWCXIIZKLJXKYOWEYPKLYHXEWCWSGZWEYPOZYHVSXEYQXBVSXCXDYQVTWACWBWSYJWBQZUTUQ
      PWTVQYQYRWCABWDXICWSYJYNWDQZDRTUSSYOYPXJKLYOWCXHXIVTWAAWBXGBCWSYJYLYSDVAV
      BSVCYOWIXNKLYOWIWHXIIZXNYHXEWHWSGZWIUUAOZYHVSXEUUBXBVSXCXDUUBVTWACWGWSYJW
      GQZVDUQPWTVQUUBUUCWHABWDXICWSYJYNYTDRTUSYOWHXMXIVTWAABWGXLCWSYJYMUUDDVFVB
      VCSVEVGXFWPYAKNVPWTVQXEWPYAOXAYOWMXRWOXTMYOWLXQKLYHXCWLXQOZXDWTVQXCUUEVTA
      BWDXICWSYJYNYTDRTVHSYOWNXSKLYHXDWNXSOZXCWTVQXDUUFWAABWDXICWSYJYNYTDRTVISV
      EVGVJVKVOVLEFCWBWGWDWSYJYSUUDYTVMVN $.
      $( [1-Feb-2008] $)
  $}

  ${
    $d w x y z A $.  $d w y z C $.  $d w y z F $.  $d w x y z U $.
    $d w x y z X $.  $d x P $.  $d z B $.
    ipblnfi.1 $e |- X = ( BaseSet ` U ) $.
    ipblnfi.7 $e |- P = ( .i ` U ) $.
    ipblnfi.9 $e |- U e. CPreHil $.
    ipblnfi.c $e |- C = <. <. + , x. >. , abs >. $.
    ipblnfi.l $e |- B = ( U BLnOp C ) $.
    ipblnfi.f $e |- F = ( x e. X |-> ( x P A ) ) $.
    $( A function ` F ` generated by varying the first argument of an inner
       product (with its second argument a fixed vector ` A ` ) is a bounded
       linear functional, i.e. a bounded linear operator from the vector space
       to ` CC ` . $)
    ipblnfi $p |- ( A e. X -> F e. B ) $=
      ( vz wcel co cfv cc wceq vy vw clno cnm cr cabs cmul cle wbr wral cns cpv
      cv wf caddc phnvi ipcl mp3an1 expcom fmptd wa simplr simprl eqid syl11anc
      cnv nvscl simprr simpll cphl ipdir syl111anc ipassi opreq1d eqtrd adantll
      w3a mpan nvgcl sylan anasss opreq1 oprex fvmpt ad2antrl ad2antll opreq12d
      syl opreq2d 3eqtr4d r19.21aivva r19.21aiva cnnvba cnnvg cnnvs islno mp2an
      wb cnnv sylanbrc nvcl sii ancoms adantl fveq2d recnd mulcom syl2an cnnvnm
      3brtr4d blo3i ) BHPZGFDUCQZPZBFUDRZRZUEPZOUMZGRZUFRZXPXRXORZUGQZUHUIZOHUJ
      GCPXLHSGUNZUAUMZXRFUKRZQZUBUMZFULRZQZGRZYEXSUGQZYHGRZUOQZTZUBHUJOHUJZUASU
      JZXNXLAHAUMZBEQZSGYRHPZXLYSSPZFVFPZYTXLUUAFKUPZYRBEFHIJUQURUSNUTXLYPUASXL
      YESPZVAZYOOUBHHUUEXRHPZYHHPZVAZVAZYJBEQZYEXRBEQZUGQZYHBEQZUOQZYKYNUUIUUJY
      GBEQZUUMUOQZUUNUUIYGHPZUUGXLUUJUUPTZUUIUUDUUFUUQXLUUDUUHVBZUUEUUFUUGVCZUU
      BUUDUUFUUQUUCYEXRYFFHIYFVDZVGURZVEUUEUUFUUGVHXLUUDUUHVIZFVJPUUQUUGXLVQUUR
      KYGYHBEFYIHIYIVDZJVKVRVLUUIUUOUULUUMUOUUIUUDUUFXLUUOUULTUUSUUTUVCYEXRBEYF
      FYIHIUVDUVAJKVMVLVNVOUUIYJHPZYKUUJTUUEUUFUUGUVEUUEUUFVAUUQUUGUVEUUDUUFUUQ
      XLUVBVPUUBUUQUUGUVEUUCYGYHFYIHIUVDVSURVTWAAYJYSUUJHGYRYJBEWBNYJBEWCWDWHUU
      IYLUULYMUUMUOUUIXSUUKYEUGUUFXSUUKTZUUEUUGAXRYSUUKHGYRXRBEWBNXRBEWCWDZWEWI
      UUGYMUUMTUUEUUFAYHYSUUMHGYRYHBEWBNYHBEWCWDWFWGWJWKWLUUBDVFPXNYDYQVAWRUUCD
      LWSZUAOUBYFUGGFYIUOXMDHSIDLWMUVDDLWNUVADLWOXMVDZWPWQWTUUBXLXQUUCBFXOHIXOV
      DZXAVRZXLYCOHXLUUFVAZUUKUFRZYAXPUGQZXTYBUHUUFXLUVMUVNUHUIXRBEFXOHIUVJJKXB
      XCUVLXSUUKUFUUFUVFXLUVGXDXEXLXPSPYASPYBUVNTUUFXLXPUVKXFUUFYAUUBUUFYAUEPUU
      CXRFXOHIUVJXAVRXFXPYAXGXHXJWLOXPCGFXMXOUFDHIUVJDLXIUVIMUUCUVHXKVL $.
      $( [19-Nov-2013] $) $( [12-Jan-2008] $)
  $}

  ${
    $d x A $.  $d x B $.  $d s t x P $.  $d s t Q $.  $d x y S $.
    $d s t x y T $.  $d x U $.  $d s t x y X $.  $d s t x y Y $.
    ip2eqi.1 $e |- X = ( BaseSet ` U ) $.
    ip2eqi.7 $e |- P = ( .i ` U ) $.
    ip2eqi.u $e |- U e. CPreHil $.
    $( Two vectors are equal iff their inner products with all other vectors
       are equal. $)
    ip2eqi $p |- ( ( A e. X /\ B e. X ) -> ( A. x e. X ( x P A
                                               ) = ( x P B ) <-> A = B ) ) $=
      ( wcel co wceq cfv eqid mp3an1 opreq1 syl cc0 mpan wb wa cv wral cnsb cnv
      wi phnvi nvmcl eqeq12d rcla4v cmin cn0v simpl simpr w3a ipsubdi syl111anc
      cphl eqeq1d bitr3d cc ipcl syl11anc sylancom subeq0 nvmeq0 3bitr3d sylibd
      ipz opreq2 a1d r19.21aiv impbid1 ) BFJZCFJZUAZAUBZBDKZVQCDKZLZAFUCZBCLZVP
      WABCEUDMZKZBDKZWDCDKZLZWBVPWDFJZWAWGUFEUEJZVNVOWHEIUGZBCEWCFGWCNZUHOZVTWG
      AWDFVQWDLVRWEVSWFVQWDBDPVQWDCDPUIUJQVPWEWFUKKZRLZWDEULMZLZWGWBVPWDWDDKZRL
      ZWNWPVPWQWMRVPWHVNVOWQWMLZWLVNVOUMZVNVOUNEURJWHVNVOUOWSIWDBCDEWCFGWKHUPSU
      QUSVPWHWRWPTZWLWIWHXAWJWDDEFWOGWONZHVISQUTVPWEVAJZWFVAJZWNWGTVPWHVNXCWLWT
      WIWHVNXCWJWDBDEFGHVBOVCVNVOWHXDWLWIWHVOXDWJWDCDEFGHVBOVDWEWFVEVCWIVNVOWPW
      BTWJBCEWCFWOGWKXBVFOVGVHWBVTAFWBVTVQFJBCVQDVJVKVLVM $.
      $( [24-Jan-2008] $)

    $( A condition implying that two operators are equal. $)
    phoeqi $p |- ( ( S : Y --> X /\ T : Y --> X ) -> ( A. x e. X A. y e.
      Y ( x P ( S ` y ) ) = ( x P ( T ` y ) ) <-> S = T ) ) $=
      ( cv cfv co wceq wral wf wa wcel wb ralcom ffvelrn ip2eqi syl2an anandirs
      ralbidva wfn ffn eqfnfv2 bitr4d syl5bb ) ALZBLZDMZCNULUMEMZCNOZBHPAGPUPAG
      PZBHPZHGDQZHGEQZRZDEOZUPABGHUAVAURUNUOOZBHPZVBVAUQVCBHUSUTUMHSZUQVCTZUSVE
      RUNGSUOGSVFUTVERHGUMDUBHGUMEUBAUNUOCFGIJKUCUDUEUFUSDHUGEHUGVBVDTUTHGDUHHG
      EUHBHDEUIUDUJUK $.
      $( [25-Jan-2008] $)

    $( Every operator has at most one adjoint. $)
    ajmoi $p |- E* s ( s : Y --> X /\
           A. x e. X A. y e. Y ( ( T ` x ) Q y ) = ( x P ( s ` y ) ) ) $=
      ( vt cv wf cfv co wceq wral wa wmo wi r19.26-2 eqtr2 ralimi sylbir phoeqi
      wal biimpa sylan2 an4s gen2 feq1 fveq1 opreq2d 2ralbidv anbi12d mo4 mpbir
      eqeq2d ) HGINZOZANZEPBNZDQZVCVDVAPZCQZRZBHSAGSZTZIUAVJHGMNZOZVEVCVDVKPZCQ
      ZRZBHSAGSZTZTVAVKRZUBZMUHIUHVSIMVBVLVIVPVRVIVPTZVBVLTZVGVNRZBHSZAGSZVRVTV
      HVOTZBHSZAGSWDVHVOABGHUCWFWCAGWEWBBHVEVGVNUDUEUEUFWAWDVRABCVAVKFGHJKLUGUI
      UJUKULVJVQIMVRVBVLVIVPHGVAVKUMVRVHVOABGHVRVGVNVEVRVFVMVCCVDVAVKUNUOUTUPUQ
      URUS $.
      $( [25-Jan-2008] $)
  $}

  ${
    $d s t x y U $.  $d s t x y W $.
    ajfuni.5 $e |- A = ( U adj W ) $.
    ajfuni.u $e |- U e. CPreHil $.
    ajfuni.w $e |- W e. NrmCVec $.
    $( The adjoint function is a function. $)
    ajfuni $p |- Fun A $=
      ( vt vs vx vy wfun cba cfv cv wf cip co wceq wral eqid w3a wmo funopab wa
      copab ajmoi 3simpc immoi ax-mp mpgbir wb cnv wcel phnvi mp2an funeq mpbir
      ajfval ) AKZBLMZCLMZGNZOZVAUTHNZOZINZVBMJNZCPMZQVFVGVDMBPMZQRJVASIUTSZUAZ
      GHUEZKZVMVKHUBZGVKGHUCVEVJUDZHUBVNIJVIVHVBBUTVAHUTTZVITZEUFVKVOHVCVEVJUGU
      HUIUJAVLRZUSVMUKBULUMCULUMVRBEUNFIJGAVIVHBCUTVAHVPVATVQVHTDURUOAVLUPUIUQ
      $.
      $( [25-Jan-2008] $)
  $}

  ${
    ajfun.5 $e |- A = ( U adj W ) $.
    $( The adjoint function is a function.  This is not immediately apparent
       from ~ df-aj but results from the uniqueness shown by ~ ajmoi . $)
    ajfun $p |- ( ( U e. CPreHil /\ W e. NrmCVec ) -> Fun A ) $=
      ( cphl wcel cnv wfun caddc cmul cop cabs cif co wceq opreq1 syl5eq funeqd
      caj opreq2 eqid elimphu elimnvu ajfuni dedth2h ) BEFZCGFZAHUFBIJKLKZMZCSN
      ZHUIUGCUHMZSNZHBCUHUHBUIOZAUJUMABCSNUJDBUICSPQRCUKOUJULCUKUISTRULUIUKULUA
      BUBCUCUDUE $.
      $( [26-Jan-2008] $)
  $}

  ${
    $d t P $.  $d t Q $.  $d s t x y T $.  $d s t x y U $.  $d s t x y W $.
    $d s t x y X $.  $d s t y Y $.
    ajval.1 $e |- X = ( BaseSet ` U ) $.
    ajval.2 $e |- Y = ( BaseSet ` W ) $.
    ajval.3 $e |- P = ( .i ` U ) $.
    ajval.4 $e |- Q = ( .i ` W ) $.
    ajval.5 $e |- A = ( U adj W ) $.
    $( Value of the adjoint function. $)
    ajval $p |- ( ( U e. CPreHil /\ W e. NrmCVec /\ T : X --> Y ) ->
       ( A ` T ) = U. { s | ( s : Y --> X /\
          A. x e. X A. y e. Y ( ( T ` x ) Q y ) = ( x P ( s ` y ) ) ) } ) $=
      ( wcel cfv wceq cvv vt cphl cnv wf w3a cv wral copab cab cuni phnv ajfval
      co wa sylan fveq1d 3adant3 wfun ajfun funeqd mpbid cba fvex eqeltri mpan2
      fex eqid feq1 fveq1 opreq1d eqeq1d 2ralbidv 3anbi13d fvopab5 syl2an 3impa
      3anass baib abbidv unieqd 3ad2ant3 3eqtrd ) GUBQZHUCQZIJFUDZUEFCRZFIJUAUF
      ZUDZJIKUFZUDZAUFZWGRZBUFZEUMZWKWMWIRDUMZSZBJUGAIUGZUEZUAKUHZRZWEWJWKFRZWM
      EUMZWOSZBJUGAIUGZUEZKUIZUJZWJXDUNZKUIZUJZWCWDWFWTSWEWCWDUNZFCWSWCGUCQWDCW
      SSGUKABUACDEGHIJKLMNOPULUOZUPUQWCWDWEWTXGSZXKWSURZFTQZXMWEXKCURXNCGHPUSXK
      CWSXLUTVAWEITQXOIGVBRTLGVBVCVDIJTFVFVEWRXEUAKFTWSWSVGWGFSZWHWEWQXDWJIJWGF
      VHXPWPXCABIJXPWNXBWOXPWLXAWMEWKWGFVIVJVKVLVMVNVOVPWEWCXGXJSWDWEXFXIWEXEXH
      KXEWEXHWEWJXDVQVRVSVTWAWB $.
      $( [18-May-2009] $) $( [25-Jan-2008] $)
  $}


