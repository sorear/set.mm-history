$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Schroeder-Bernstein Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x D $.  $d x f $.  $d x g $.
    sbthlem.1 $e |- A e. _V $.
    sbthlem.2 $e |- D = { x |
                   ( x C_ A /\ ( g " ( B \ ( f " x ) ) ) C_ ( A \ x ) ) } $.
    $( Lemma for ~ sbth . $)
    sbthlem1 $p |- U. D C_ ( A \ ( g " ( B \ ( f " U. D ) ) ) ) $=
      ( cuni cv cima cdif wss unissb wcel wa abeq2i wi ssconb exbiri difss
      sstr2 mpi syl5 pm2.43d imp sylbi elssuni imass2 sscon 3syl sstrd mprgbir
      ) DIZBFJZCEJZUNKZLZKZLZMAJZUTMADADUTNVADOZVABUOCUPVAKZLZKZLZUTVBVABMZVEBV
      ALZMZPZVAVFMZVJADHQVGVIVKVGVIVKVGVEBMZVIVKRVIVGVLVKVIVAVEBSTVIVHBMVLBVAUA
      VEVHBUBUCUDUEUFUGVBURVDMZUSVEMVFUTMVBVAUNMVCUQMVMVADUHVAUNUPUIVCUQCUJUKUR
      VDUOUIUSVEBUJUKULUM $.
      $( [22-Mar-1998] $)

    $( Lemma for ~ sbth . $)
    sbthlem2 $p |- ( ran g C_ A ->
                     ( A \ ( g " ( B \ ( f " U. D ) ) ) ) C_ U. D ) $=
      ( cv crn wss cuni cima cdif wcel wa cab sbthlem1 imass2 ax-mp sscon wb
      ssconb wi imassrn sstr2 difss sylancl mpbiri jctil ssexi wceq sseq1
      difeq2 sseq2d imaeq2 difeq2d 3syl bitrd anbi12d elab sylibr syl6eleqr
      elssuni syl ) FIZJZBKZBVFCEIZDLZMZNZMZNZDOVNVJKVHVNAIZBKZVFCVIVOMZNZMZBVO
      NZKZPZAQZDVHVNBKZVFCVIVNMZNZMZBVNNZKZPZVNWCOVHWIWDVHWIVNBWGNKZWGVMKZWKWFV
      LKZWLVKWEKZWMVJVNKWNABCDEFGHRVJVNVISTVKWECUATWFVLVFSTWGVMBUATWGBKZWDWIWKU
      BVHWGVNBUCWGVGKVHWOUDVFWFUEWGVGBUFTBVMUGZUHUIWPUJWBWJAVNVNBGWPUKVOVNULZVP
      WDWAWIVOVNBUMWQWAVSWHKZWIWQVTWHVSVOVNBUNUOWQVRWFULVSWGULWRWIUBWQVQWECVOVN
      VIUPUQVRWFVFUPVSWGWHUMURUSUTVAVBHVCVNDVDVE $.
      $( [22-Mar-1998] $)

    $( Lemma for ~ sbth . $)
    sbthlem3 $p |- ( ran g C_ A ->
                  ( g " ( B \ ( f " U. D ) ) ) = ( A \ U. D ) ) $=
      ( cv crn wss cuni cdif cima wa wceq sbthlem2 sbthlem1 jctil eqss sylibr
      difeq2d wi imassrn sstr2 ax-mp dfss4 sylib eqtr2d ) FIZJZBKZBDLZMBBUJCEIU
      MNMZNZMZMZUOULUMUPBULUMUPKZUPUMKZOUMUPPULUSURABCDEFGHQABCDEFGHRSUMUPTUAUB
      ULUOBKZUQUOPUOUKKULUTUCUJUNUDUOUKBUEUFUOBUGUHUI $.
      $( [22-Mar-1998] $)

    $( Lemma for ~ sbth . $)
    sbthlem4 $p |- ( ( ( dom g = B /\ ran g C_ A ) /\ Fun `' g ) ->
                  ( `' g " ( A \ U. D ) ) = ( B \ ( f " U. D ) ) ) $=
      ( cv cdm wceq crn wss wa ccnv wfun cuni cima cdif cres difss sseq2
      mpbiri ssdmres sylib dfdm4 syl5reqr funcnvres sbthlem3 reseq2d sylan9eqr
      rneqd sylan9eq anassrs df-ima syl6reqr ) FIZJZCKZUQLBMZNUQOZPZNCEIDQZRZSZ
      VABVCSZTZLZVAVFRUSUTVBVEVHKUSUTVBNZVEUQVETZOZLZVHUSVJJZVEVLUSVEURMZVMVEKU
      SVNVECMCVDUAURCVEUBUCVEUQUDUEVJUFUGVIVKVGVBUTVKVAUQVERZTVGVEUQUHUTVOVFVAA
      BCDEFGHUIUJUKULUMUNVAVFUOUP $.
      $( [27-Mar-1998] $)

    ${
      $d x H $.
      sbthlem.3 $e |- H = ( ( f |` U. D ) u. ( `' g |` ( A \ U. D ) ) ) $.
      $( Lemma for ~ sbth . $)
      sbthlem5 $p |- ( ( dom f = A /\ ran g C_ A ) -> dom H = A ) $=
        ( cv cdm wceq crn wss wa cuni cdif cun cin cima sbthlem1 difss sstri
        sseq2 mpbiri dfss sylib uneq1d imassrn sbthlem3 sseq1d mpbii uneq2d
        sylan9eq cres ccnv dmeqi dmun dmres df-rn eqcomi ineq2i eqtri uneq12i
        3eqtri syl6reqr undif mpbi syl6eq ) EKZLZBMZFKZNZBOZPZGLZDQZBVSRZSZBVQW
        AVSVLTZVTVOTZSZVRVMVPWAWBVTSWDVMVSWBVTVMVSVLOZVSWBMVMWEVSBOZVSBVNCVKVSU
        ARZUAZRBABCDEFHIUBBWHUCUDZVLBVSUEUFVSVLUGUHUIVPVTWCWBVPVTVOOZVTWCMVPWHV
        OOWJVNWGUJVPWHVTVOABCDEFHIUKULUMVTVOUGUHUNUOVRVKVSUPZVNUQZVTUPZSZLWKLZW
        MLZSWDGWNJURWKWMUSWOWBWPWCVKVSUTWPVTWLLZTWCWLVTUTWQVOVTVOWQVNVAVBVCVDVE
        VFVGWFWABMWIVSBVHVIVJ $.
        $( [22-Mar-1998] $)

      $( Lemma for ~ sbth . $)
      sbthlem6 $p |- ( ( ran f C_ B /\
               ( ( dom g = B /\ ran g C_ A ) /\ Fun `' g ) ) -> ran H = B ) $=
        ( cv cdm wceq crn wss wa ccnv wfun cuni cima cdif cun cres sbthlem4
        df-ima syl5reqr uneq2d rnun rneqi uneq1i 3eqtr4i syl6reqr wi imassrn
        sstr2 ax-mp undif sylib sylan9eqr ) FKZLCMUTNBOPUTQZRPZEKZNZCOZGNZVCDSZ
        TZCVHUAZUBZCVBVJVHVABVGUAZUCZNZUBZVFVBVIVMVHVBVAVKTVIVMABCDEFHIUDVAVKUE
        UFUGVCVGUCZVLUBZNVONZVMUBVFVNVOVLUHGVPJUIVHVQVMVCVGUEUJUKULVEVHCOZVJCMV
        HVDOVEVRUMVCVGUNVHVDCUOUPVHCUQURUS $.
        $( [27-Mar-1998] $)

      $( Lemma for ~ sbth . $)
      sbthlem7 $p |- ( ( Fun f /\ Fun `' g ) -> Fun H ) $=
        ( cv wfun ccnv wa cuni cres cdif cun cdm cin c0 wceq wss dmres inss1
        eqsstri ssrin ax-mp sslin sstri difdisj sseqtri ss0 funun mpan2 funres
        syl2an wb funeq sylibr ) EKZLZFKMZLZNVADOZPZVCBVEQZPZRZLZGLZVFLZVHLZVJV
        BVDVLVMNVFSZVHSZTZUAUBZVJVPUAUCVQVPVEVGTZUAVPVEVOTZVRVNVEUCVPVSUCVNVEVA
        SZTVEVAVEUDVEVTUEUFVNVEVOUGUHVOVGUCVSVRUCVOVGVCSZTVGVCVGUDVGWAUEUFVOVGV
        EUIUHUJVEBUKULVPUMUHVFVHUNUOVEVAUPVGVCUPUQGVIUBVKVJURJGVIUSUHUT $.
        $( [27-Mar-1998] $)

      $( Lemma for ~ sbth . $)
      sbthlem8 $p |- ( ( Fun `' f /\ ( ( ( Fun g /\ dom g = B ) /\ ran g C_ A )
                     /\ Fun `' g ) ) -> Fun `' H ) $=
        ( cv ccnv wfun cdm wceq wa crn wss cuni cres cdif cun cin c0 funres11
        funcnvcnv syl ad3antrrr anim12i cima ineq12 df-ima df-rn eqtr2i
        sbthlem4 eqtri syl5eqr sylancr difdisj syl6eq adantlll adantl funun
        syl11anc wb cnveqi cnvun funeq ax-mp sylibr ) EKZLMZFKZMZVMNCOZPVMQBRZP
        VMLZMZPZPZVKDSZTZLZVQBWAUAZTZLZUBZMZGLZMZVTWCMZWFMZPWCNZWFNZUCZUDOZWHVL
        WKVSWLWAVKUEVNWLVOVPVRVNVQLMWLVMUFWDVQUEUGUHUIVSWPVLVOVPVRWPVNVOVPPVRPZ
        WOVKWAUJZCWRUAZUCZUDWMWROWNWSOWOWTOWQWMWRWNWSUKWRWBQWMVKWAULWBUMUNWQVQW
        DUJZWSWNABCDEFHIUOXAWEQWNVQWDULWEUMUPUQURWRCUSUTVAVBWCWFVCVDWIWGOWJWHVE
        WIWBWEUBZLWGGXBJVFWBWEVGUPWIWGVHVIVJ $.
        $( [27-Mar-1998] $)

      $( Lemma for ~ sbth . $)
      sbthlem9 $p |- ( ( f : A -1-1-> B /\ g : B -1-1-> A ) ->
                    H : A -1-1-onto-> B ) $=
        ( cv wfun cdm wceq wa crn wss ccnv wf1 wf1o sbthlem7 sbthlem5 adantrl
        anim12i an42s adantlr sbthlem8 adantll sbthlem6 df-rn syl5eqr simpr
        anim1i sylanr1 jca32 wf df-f1 wfn df-f df-fn anbi1i bitri anbi12i
        dff1o4 3imtr4i ) EKZLZVFMBNZOZVFPCQZOZVFRLZOZFKZLZVNMCNZOZVNPBQZOZVNRLZ
        OZOZGLZGMBNZOZGRZLZWFMZCNZOZOZBCVFSZCBVNSZOBCGTZWBWEWGWIVKWAWEVLVIWAWEV
        JVGVTVHVSWEVGVTOWCVHVSOWDABCDEFGHIJUAVHVRWDVQABCDEFGHIJUBUCUDUEUFUFVLWA
        WGVKABCDEFGHIJUGUHVKWAWIVLVJWAWIVIVJVPVROZVTWIVSVJWOVTOOGPCWHABCDEFGHIJ
        UIGUJUKVQVPVRVOVPULUMUNUHUFUOWLVMWMWAWLBCVFUPZVLOVMBCVFUQWPVKVLWPVFBURZ
        VJOVKBCVFUSWQVIVJVFBUTVAVBVAVBWMCBVNUPZVTOWACBVNUQWRVSVTWRVNCURZVROVSCB
        VNUSWSVQVRVNCUTVAVBVAVBVCWNGBURZWFCURZOWKBCGVDWTWEXAWJGBUTWFCUTVCVBVE
        $.
        $( [28-Mar-1998] $)

      ${
        $d f g A $.  $d f g B $.
        sbthlem.4 $e |- B e. _V $.
        $( Lemma for ~ sbth . $)
        sbthlem10 $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
          ( cdom wbr wa cv wf1 wex cen brdom anbi12i eeanv bitr4i wf1o
          sbthlem9 f1oen syl 19.23aivv sylbi ) BCLMZCBLMZNZBCEOPZCBFOPZNZFQEQZB
          CRMZUKULEQZUMFQZNUOUIUQUJURBCEKSCBFHSTULUMEFUAUBUNUPEFUNBCGUCUPABCDEF
          GHIJUDBCGHUEUFUGUH $.
          $( [20-Jan-2007] $) $( [28-Mar-1998] $)
      $}
    $}

  $}

  ${
    $d x y z w f g A $.  $d x y z w f g B $.
    $( Schroeder-Bernstein Theorem.  Theorem 18 of [Suppes] p. 95.  This
       theorem states that if set ` A ` is smaller (has lower cardinality) than
       ` B ` and vice-versa, then ` A ` and ` B ` are equinumerous (have the
       same cardinality).  The interesting thing is that this can be proved
       without invoking the Axiom of Choice, as we do here, but the proof as
       you can see is quite difficult.  (The theorem can be proved more easily
       if we allow AC.) The main proof consists of lemmas ~ sbthlem1 through
       ~ sbthlem10 ; this final piece mainly changes bound variables to
       eliminate the hypotheses of ~ sbthlem10 .  We follow closely the proof
       in Suppes, which you should consult to understand our proof at a higher
       level.  Note that Suppes' proof, which is credited to J. M. Whitaker,
       does not require the Axiom of Infinity. $)
    sbth $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
      ( vz vw vx vy vg vf cdom wbr wa cen cvv wcel wi cv wceq breq1 breq2
      anbi12d imbi12d wss cima cdif cab cuni cres ccnv cun visset sseq1 wb
      imaeq2 difeq2d 3syl difeq2 sseq2d bitrd cbvabv eqid sbthlem10 vtocl2g
      reldom brrelexi syl2an pm2.43i ) ABIJZBAIJZKZABLJZAMNBMNVIVJOZVGVHCPZDPZI
      JZVMVLIJZKZVLVMLJZOAVMIJZVMAIJZKZAVMLJZOVKCDABMMVLAQZVPVTVQWAWBVNVRVOVSVL
      AVMIRVLAVMISTVLAVMLRUAVMBQZVTVIWAVJWCVRVGVSVHVMBAISVMBAIRTVMBALSUAEVLVMFP
      ZVLUBZGPZVMHPZWDUCZUDZUCZVLWDUDZUBZKZFUEZHGWGWNUFZUGWFUHVLWOUDUGUIZCUJWME
      PZVLUBZWFVMWGWQUCZUDZUCZVLWQUDZUBZKFEWDWQQZWEWRWLXCWDWQVLUKXDWLXAWKUBZXCX
      DWIWTQWJXAQWLXEULXDWHWSVMWDWQWGUMUNWIWTWFUMWJXAWKUKUOXDWKXBXAWDWQVLUPUQUR
      TUSWPUTDUJVAVBABIVCVDBAIVCVDVEVF $.
      $( [20-Jan-2007] $) $( [8-Jun-1998] $)
  $}

  $( Schroeder-Bernstein Theorem and its converse. $)
  sbthbg $p |- ( B e. C -> ( ( A ~<_ B /\ B ~<_ A ) <-> A ~~ B ) ) $=
    ( wcel cdom wbr wa cen sbth wi endom a1i ensymg syl6 jcad impbid2 ) BCDZABE
    FZBAEFZGABHFZABIQTRSTRJQABKLQTBAHFSABCMBAKNOP $.
    $( [8-Jun-1998] $)

  ${
    $d x y $.
    $( Schroeder-Bernstein Theorem in class form. $)
    sbthcl $p |- ~~ = ( ~<_ i^i `' ~<_ ) $=
      ( vx vy cen cdom ccnv cin relen wrel reldom relin1 ax-mp cv wbr wa cop
      wcel cvv wb visset sbthbg df-br opelcnv bitr4i anbi12i elin 3bitr3ri
      eqrelriv ) ABCDDEZFZGDHUIHIDUHJKALZBLZDMZUKUJDMZNZUJUKCMZUJUKOZUIPZUPCPUK
      QPUNUORBSZUJUKQTKUNUPDPZUPUHPZNUQULUSUMUTUJUKDUAUMUKUJODPUTUKUJDUAUJUKDAS
      URUBUCUDUPDUHUEUCUJUKCUAUFUG $.
      $( [28-Mar-1998] $)
  $}

  $( Alternate definition of strict dominance.  Compare Definition 3 of
     [Suppes] p. 97. $)
  dfsdom2 $p |- ~< = ( ~<_ \ `' ~<_ ) $=
    ( csdm cdom cen cdif ccnv cin df-sdom sbthcl difeq2i difin 3eqtri ) ABCDBBB
    EZFZDBLDGCMBHIBLJK $.
    $( [31-Mar-1998] $)

  ${
    brsdom2.1 $e |- A e. _V $.
    brsdom2.2 $e |- B e. _V $.
    $( Alternate definition of strict dominance.  Definition 3 of [Suppes]
       p. 97. $)
    brsdom2 $p |- ( A ~< B <-> ( A ~<_ B /\ -. B ~<_ A ) ) $=
      ( cop csdm wcel cdom ccnv cdif wbr wn wa dfsdom2 eleq2i df-br opelcnv
      bitr4i notbii anbi12i eldif 3bitr4i ) ABEZFGUCHHIZJZGZABFKABHKZBAHKZLZMZF
      UEUCNOABFPUJUCHGZUCUDGZLZMUFUGUKUIUMABHPUHULUHBAEHGULBAHPABHCDQRSTUCHUDUA
      RUB $.
      $( [27-Jul-2004] $)
  $}

  $( Strict dominance is not symmetric.  Theorem 21(ii) of [Suppes] p. 97. $)
  sdomnsym $p |- ( A ~< B -> -. B ~< A ) $=
    ( csdm wbr cen sdomnen cdom sbth sdomdom syl2an mtand ) ABCDZBACDZABEDZABFA
    BGDBAGDNLMABHABIBAIJK $.
    $( [8-Jun-1998] $)

  $( Theorem 22(i) of [Suppes] p. 97. $)
  domnsym $p |- ( A ~<_ B -> -. B ~< A ) $=
    ( cdom wbr csdm cen wo wn brdom2 sdomnsym cvv wcel relsdom brrelexi
    sdomnen ensymg con3d sylc con2i jaoi sylbi ) ABCDABEDZABFDZGBAEDZHZABIUBUEU
    CABJUDUCUDBKLZBAFDZHUCHBAEMNBAOUFUCUGABKPQRSTUA $.
    $( [10-Jun-1998] $)

  $( Any set dominates the empty set. $)
  0dom $p |- (/) ~<_ A $=
    ( c0 cvv wcel wss cdom wbr 0ex 0ss ssdomg mp2 ) BCDBAEBAFGHAIBACJK $.
    $( [26-Oct-2003] $)

  $( A set dominated by the empty set is empty. $)
  dom0 $p |- ( A ~<_ (/) <-> A = (/) ) $=
    ( c0 cdom wbr wa cen wceq 0dom biantru cvv wcel wb 0ex sbthbg ax-mp en0
    3bitri ) ABCDZRBACDZEZABFDZABGSRAHIBJKTUALMABJNOAPQ $.
    $( [22-Nov-2004] $)

  $( A set strictly dominates the empty set iff it is not empty. $)
  0sdomg $p |- ( A e. B -> ( (/) ~< A <-> A =/= (/) ) ) $=
    ( wcel c0 cen wbr wn wceq csdm wne ensymg 0ex ensym impbid1 en0 syl6bb
    notbid cdom brsdom 0dom mpbiran df-ne 3bitr4g ) ABCZDAEFZGZADHZGDAIFZADJUDU
    EUGUDUEADEFZUGUDUEUIDABKADLMNAOPQUHDARFUFDASATUAADUBUC $.
    $( [25-Nov-2006] $) $( [23-Mar-2006] $)

  ${
    0sdom.1 $e |- A e. _V $.
    $( A set strictly dominates the empty set iff it is not empty. $)
    0sdom $p |- ( (/) ~< A <-> A =/= (/) ) $=
      ( cvv wcel c0 csdm wbr wne wb 0sdomg ax-mp ) ACDEAFGAEHIBACJK $.
      $( [26-Nov-2006] $) $( [29-Jul-2004] $)
  $}

  $( The empty set does not strictly dominate any set. $)
  sdom0 $p |- -. A ~< (/) $=
    ( c0 cdom wbr csdm wn 0dom domnsym ax-mp ) BACDABEDFAGBAHI $.
    $( [26-Oct-2003] $)

  $( Transitivity of strict dominance and dominance.  Theorem 22(iii) of
     [Suppes] p. 97. $)
  sdomdomtr $p |- ( C e. D -> ( ( A ~< B /\ B ~<_ C ) -> A ~< C ) ) $=
    ( wcel csdm wbr cdom wa cen wn sdomnen ad2antrl wi domtr sdomdom sylan wo
    brdom2 df-or bitri sylib adantl ensymg endom syl6 sylan9r jctird simprr
    sbth jcad entr mt3d ex ) CDEZABFGZBCHGZIZACFGZUOURIZUSABJGZUPVAKUOUQABLMUTU
    SKZACJGZCBJGZIVAUTVBVCVDURVBVCNZUOURACHGZVEABHGZUQVFUPABCOABPZQVFUSVCRVEACS
    USVCTUAUBZUCUTVBCBHGZUQIVDUTVBVJUQUTVBCAHGZVGIVJUTVBVKVGURVBVCUOVKVIUOVCCAJ
    GVKACDUDCAUEUFUGUPVGUOUQVHMUHCABOUFUOUPUQUIUHCBUJUFUKACBULUFUMUN $.
    $( [26-Oct-2003] $)

  $( Transitivity of strict dominance and equinumerosity.  Exercise 11 of
     [Suppes] p. 98. $)
  sdomentr $p |- ( C e. D -> ( ( A ~< B /\ B ~~ C ) -> A ~< C ) ) $=
    ( wcel csdm wbr cdom cen sdomdomtr endom sylan2i ) CDEABFGBCHGACFGBCIGABCDJ
    BCKL $.
    $( [26-Oct-2003] $)

  $( Transitivity of equinumerosity and strict dominance. $)
  ensdomtr $p |- ( ( A ~~ B /\ B ~< C ) -> A ~< C ) $=
    ( cvv wcel cen wbr csdm wa wi cdom wn endomtr ex adantl ensymg imp entr
    syl con3d anim12d brsdom 3imtr4g expimpd relsdom brrelexi con3i pm2.21d
    adantld pm2.61i ) BDEZABFGZBCHGZIACHGZJUKULUMUNUKULIZBCKGZBCFGZLZIACKGZACFG
    ZLZIUMUNUOUPUSURVAULUPUSJUKULUPUSABCMNOUOUTUQUOBAFGZUTUQJUKULVBABDPQVBUTUQB
    ACRNSTUABCUBACUBUCUDUKLZUMUNULVCUMUNUMUKBCHUEUFUGUHUIUJ $.
    $( [26-Oct-2003] $)

  $( Strict dominance is irreflexive.  Theorem 21(i) of [Suppes] p. 97. $)
  sdomirr $p |- -. A ~< A $=
    ( cvv wcel csdm wbr wn cen sdomnen enrefg nsyl3 relsdom brrelexi con3i
    pm2.61i ) ABCZAADEZFPAAGEOAAHABIJPOAADKLMN $.
    $( [4-Jun-1998] $)

  $( Technical lemma for simplifying proofs involving strict dominance. $)
  sdomex $p |- ( A ~< B -> ( A e. _V /\ B e. _V ) ) $=
    ( csdm wbr cvv wcel relsdom brrelexi wn sdomirr brprc mtbiri con4i jca ) AB
    CDZAEFBEFZABCGHPOPIOAACDAJABCKLMN $.
    $( [3-Sep-2004] $)

  $( Strict dominance is transitive.  Theorem 21(iii) of [Suppes] p. 97. $)
  sdomtr $p |- ( ( A ~< B /\ B ~< C ) -> A ~< C ) $=
    ( csdm wbr cvv wcel wa wi sdomex simprd cdom sdomdomtr sdomdom sylan2i syl
    anabsi7 ) ABDEZBCDEZACDEZSCFGZRSHTISBFGUABCJKUARBCLETSABCFMBCNOPQ $.
    $( [9-Jun-1998] $)

  $( Strict dominance has no 2-cycle loops. $)
  sdomn2lp $p |- -. ( A ~< B /\ B ~< A ) $=
    ( csdm wbr wa sdomirr sdomtr mto ) ABCDBACDEAACDAFABAGH $.
    $( [6-May-2008] $)

  $( Transitivity of dominance and strict dominance.  Theorem 22(ii) of
     [Suppes] p. 97. $)
  domsdomtr $p |- ( ( A ~<_ B /\ B ~< C ) -> A ~< C ) $=
    ( cdom wbr csdm cen wo wi brdom2 sdomtr ex cvv wcel relsdom brrelexi wa wn
    endomtr adantl ensymg entr syl6 imp con3d anim12d brsdom 3imtr4g syl
    pm2.43b jaoi sylbi ) ABDEZBCFEZACFEZUMABFEZABGEZHUNUOIZABJUPURUQUPUNUOABCKL
    UQUNUOUNBMNZUQURIBCFOPUSUQURUSUQQZBCDEZBCGEZRZQACDEZACGEZRZQUNUOUTVAVDVCVFU
    QVAVDIUSUQVAVDABCSLTUTVEVBUSUQVEVBIZUSUQBAGEZVGABMUAVHVEVBBACUBLUCUDUEUFBCU
    GACUGUHLUIUJUKULUD $.
    $( [10-Jun-1998] $)

  $( Equality-like theorem for equinumerosity. $)
  enen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~~ C <-> B ~~ C ) ) $=
    ( wcel cen wbr wa wi ensymg imp entr ex syl adantll impbid ) BDEZABFGZHZACF
    GZBCFGZSBAFGZTUAIQRUBABDJKUBTUABACLMNSUATRUATQABCLOMP $.
    $( [18-Dec-2003] $)

  $( Equality-like theorem for equinumerosity. $)
  enen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~~ A <-> C ~~ B ) ) $=
    ( wcel cen wbr wa wi entr expcom adantl ex ensymg imp syl5com impbid ) BDEZ
    ABFGZHZCAFGZCBFGZSUAUBIRUASUBCABJKLUBBAFGZUATUBUCUACBAJMRSUCABDNOPQ $.
    $( [18-Dec-2003] $)

  $( Equality-like theorem for equinumerosity and dominance. $)
  domen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~<_ C <-> B ~<_ C ) ) $=
    ( wcel cen wbr wa cdom wi ensymg imp endomtr ex syl adantl impbid ) BDEZABF
    GZHZACIGZBCIGZTBAFGZUAUBJRSUCABDKLUCUAUBBACMNOSUBUAJRSUBUAABCMNPQ $.
    $( [8-Nov-2003] $)

  $( Equality-like theorem for equinumerosity and dominance. $)
  domen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~<_ A <-> C ~<_ B ) ) $=
    ( wcel cen wbr wa cdom wi domentr expcom adantl ex ensymg imp syl5com
    impbid ) BDEZABFGZHZCAIGZCBIGZTUBUCJSUBTUCCABKLMUCBAFGZUBUAUCUDUBCBAKNSTUDA
    BDOPQR $.
    $( [7-Jul-2004] $) $( [8-Nov-2003] $)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  sdomen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~< C <-> B ~< C ) ) $=
    ( wcel cen wbr wa csdm wi ensymg imp ensdomtr ex syl adantll impbid ) BDEZA
    BFGZHZACIGZBCIGZTBAFGZUAUBJRSUCABDKLUCUAUBBACMNOTUBUASUBUARABCMPNQ $.
    $( [8-Nov-2003] $)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  sdomen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~< A <-> C ~< B ) ) $=
    ( cvv wcel cen wbr wa csdm wb wi sdomentr exp3a com23 imp adantll ensymg
    syl9r imp31 impbid expl wn relen brrelexi con3i pm2.21d adantld pm2.61i )
    AEFZBDFZABGHZICAJHZCBJHZKZLUJUKULUOUJUKIULIUMUNUKULUMUNLZUJUKULUPUKUMULUNUK
    UMULUNCABDMNOPQUJUKULUNUMLZUKULBAGHZUJUQABDRUJUNURUMUJUNURUMCBAEMNOSTUAUBUJ
    UCZULUOUKUSULUOULUJABGUDUEUFUGUHUI $.
    $( [7-Jul-2004] $) $( [8-Nov-2003] $)

  ${
    $d A x $.
    $( Every ordinal number is numerable.  (Lemma for future theorems.)
       (Contributed by Mario Carneiro, 7-Jan-2013.) $)
    onenon $p |- ( A e. On -> E. x e. On x ~~ A ) $=
      ( con0 wcel cen wbr cv wrex enrefg breq1 rcla4ev mpdan ) BCDBBEFZAGZBEFZA
      CHBCIOMABCNBBEJKL $.
      $( [7-Jan-2013] $)
  $}

  $( Dominance is trichotomous in the restricted case of ordinal numbers.
     (Contributed by Jeff Hankins, 24-Oct-2009.) $)
  domtriord $p |- ( ( A e. On /\ B e. On ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( con0 wcel wa cdom wbr csdm wn cen wi sbth expcom a1i iman brsdom notbii
    bicomi bitri syl6ib wss onelss ssdomg sylan9r con3d wb word ordtri1 eloni
    syl2an ancoms sylibrd adantl syld ensymg adantr endom syl6 jcad syl6ibr
    con1d impbid ) ACDZBCDZEZABFGZBAHGZIZVEVFBAFGZBAJGZKZVHVFVKKVEVIVFVJBALMNVK
    VIVJIZEZIZVHVIVJOVHVNVGVMBAPZQRSTVEVFVGVEVFIZVMVGVEVPVIVLVEVPBAUAZVIVEVPABD
    ZIZVQVEVRVFVDVRABUAVCVFBAUBABCUCUDUEVDVCVQVSUFZBUGAUGVTVDVCBAUHBUIAUIUJUKUL
    VDVQVIKVCBACUCUMUNVEVJVFVEVJABJGZVFVCVJWAKVDBACUOUPABUQURUEUSVOUTVAVB $.
    $( [24-Oct-2009] $)

  $( Strict dominance implies ordinal membership.  (Contributed by Mario
     Carneiro, 13-Jan-2013.) $)
  sdomel $p |- ( ( A e. On /\ B e. On ) -> ( A ~< B -> A e. B ) ) $=
    ( con0 wcel csdm wbr wi wa wss cdom wn ssdomg adantr ontri1 domtriord
    3imtr3d con4d ancoms ) BCDZACDZABEFZABDZGSTHZUBUAUCBAIZBAJFZUBKUAKSUDUEGTBA
    CLMBANBAOPQR $.
    $( [13-Jan-2013] $)

  ${
    $d f g z A $.  $d f g z B $.
    $( There exists a mapping from a set onto any (non-empty) set that it
       dominates. $)
    fodomr $p |- ( ( A e. C /\
                 (/) ~< B /\ B ~<_ A ) -> E. f f : A -onto-> B ) $=
      ( vz vg wcel c0 csdm wbr cdom w3a cvv cv wex wf1 wfo elisset 3ad2ant1 wb
      reldom brrelexi wne 0sdomg n0 syl6bb syl biimpac 3adant1 brdomg biimpa
      3adant2 wi wa ccnv crn cdif csn cxp cun xpexg difexg snex sylancl visset
      cnvex jctil unexb sylib foeq1 cla4egv wfn wceq df-fo wfun cdm df-fn cin
      funun wf df-f1 simprbi fconst ffun ax-mp jctir df-rn eqcomi snnz dmxp
      ineq12i difdisj eqtri adantl wss f1f frn undif dmun uneq1i uneq2i
      3eqtr2i syl5eq sylanbrc fdm dfdm4 syl5eqr uneq1d 0ss xpeq1 xp0r syl6eq
      rneqd rn0 sseq1d mpbiri a1d rnxp adantr snssi eqsstrd ex pm2.61ine
      ssequn2 sylan9eqr rnun syl5 expdimp 19.23adv syl3c ) ACGZHBIJZBAKJZLAMGZE
      NZBGZEOZBAFNZPZFOZABDNZQZDOZYKYLYNYMACRSYLYMYQYKYMYLYQYMBMGZYLYQTBAKUAUBU
      UDYLBHUCYQBMUDEBUEUFUGUHUIYKYMYTYLYKYMYTBACFUJUKULYNYPYTUUCUMZEYNYPUUEYNY
      PUNYSUUCFYNYPYSUUCYNABYRUOZAYRUPZUQZYOURZUSZUTZQZUUCYPYSUNZYNUUKMGZUULUUC
      UMYNUUFMGZUUJMGZUNUUNYNUUPUUOUUHMGUUIMGUUPYNUUHUUIMMVAAUUGMVBYOVCVDYRFVEV
      FVGUUFUUJVHVIUUBUULDUUKMABUUAUUKVJVKUGUULUUKAVLZUUKUPZBVMUUMABUUKVNUUQUUK
      VOZUUKVPZAVMZUUMUUKAVQYSUUSYPUUFVOZUUJVOZUNUUFVPZUUJVPZVRZHVMUUSYSUUFUUJV
      SYSUVBUVCYSBAYRVTZUVBBAYRWAWBUUHUUIUUJVTUVCUUHYOEVEZWCUUHUUIUUJWDWEWFUVFU
      UGUUHVRHUVDUUGUVEUUHUUGUVDYRWGZWHUUIHUCUVEUUHVMYOUVHWIUUHUUIWJWEZWKUUGAWL
      WMVDWNYSUVAYPYSUUGUUHUTZAUUTYSUUGAWOZUVKAVMYSUVGUVLBAYRWPZBAYRWQUGUUGAWRV
      IUUTUVDUVEUTUUGUVEUTUVKUUFUUJWSUUGUVDUVEUVIWTUVEUUHUUGUVJXAXBXCWNXDUUMUUF
      UPZUUJUPZUTZBUURYSYPUVPBUVOUTZBYSUVNBUVOYSYRVPZBUVNYSUVGUVRBVMUVMBAYRXEUG
      YRXFXGXHYPUVOBWOZUVQBVMYPUVSUMUUHHUUHHVMZUVSYPUVTUVSHBWOBXIUVTUVOHBUVTUVO
      HUPHUVTUUJHUVTUUJHUUIUSHUUHHUUIXJUUIXKXLXMXNXLXOXPXQUUHHUCZYPUVSUWAYPUNUV
      OUUIBUWAUVOUUIVMYPUUHUUIXRXSYPUUIBWOUWAYOBXTWNYAYBYCUVOBYDVIYEUUFUUJYFXCX
      DYGYHYIYBYIYJ $.
      $( [23-Mar-2006] $)
  $}

  ${
    $d x y z f A $.
    canth2.1 $e |- A e. _V $.
    $( Cantor's Theorem.  No set is equinumerous to its power set.
       Specifically, any set has a cardinality (size) strictly less than the
       cardinality of its power set.  For example, the cardinality of real
       numbers is the same as the cardinality of the power set of integers, so
       real numbers cannot be put into a one-to-one correspondence with
       integers.  Theorem 23 of [Suppes] p. 97.  For the function version, see
       ~ canth . $)
    canth2 $p |- A ~< ~P A $=
      ( vx vy vf cpw csdm wbr cdom cen wn brsdom cvv wcel cv csn visset snelpw
      biimpi wceq wb wa sneqr sneq impbii a1i dom2 ax-mp wf1o wex wfo canth
      f1ofo mto nex pwex bren mtbir mpbir2an ) AAFZGHAUTIHZAUTJHZKAUTLAMNVABCDA
      UTCOZPZDOZPZMVCANZVDUTNVCACQZRSVDVFTZVCVETZUAVGVEANUBVIVJVCVEVHUCVCVEUDUE
      UFUGUHVBAUTEOZUIZEUJVLEVLAUTVKUKAVKBULAUTVKUMUNUOAUTEABUPUQURUS $.
      $( [1-Apr-198] $)
  $}

  ${
    $d x A $.
    $( Cantor's theorem with the sethood requirement expressed as an
       antecedent.  Theorem 23 of [Suppes] p. 97. $)
    canth2g $p |- ( A e. B -> A ~< ~P A ) $=
      ( vx cv cpw csdm wbr wceq wb pweq breq12 mpdan visset canth2 vtoclg ) CDZ
      PEZFGZAAEZFGZCABPAHQSHRTIPAJPAQSFKLPCMNO $.
      $( [7-Nov-2003] $)
  $}

  $( The power set of the union of a set does not belong to the set.  This
     theorem provides a way of constructing a new set that doesn't belong to a
     given set. $)
  pwuninel $p |- -. ~P U. A e. A $=
    ( cvv wcel cuni cpw wn csdm wbr sdomirr wi uniexb wss cdom ssdom2g
    domsdomtr canth2g sylan2 expcom syld elssuni syl5 sylbi mtoi elisset pwexb
    bitri sylibr con3i pm2.61i ) ABCZADZEZACZFUJUMULULGHZULIUJUKBCZUMUNJAKZUOUL
    UKLZUNUMUOUQULUKMHZUNULUKBNURUOUNURUKULGHUNUOULUKULOUKBPQRSULATUAUBUCUMUJUM
    ULBCZUJULAUDUJUOUSUPUKUEUFUGUHUI $.
    $( [27-Jun-2008] $)

  $( The power set of the power set of the union of a set does not belong to
     the set.  This theorem provides a way of constructing a new set that
     doesn't belong to a given set. $)
  2pwuninel $p |- -. ~P ~P U. A e. A $=
    ( cuni cvv wcel cpw wn csdm wbr sdomirr wss cdom ssdom2g domsdomtr ex
    canth2g pwexb sylbi sdomtr syl11anc syl5com syld elssuni syl5 mtoi elisset
    bitri sylibr con3i pm2.61i ) ABZCDZUJEZEZADZFUKUNUMUMGHZUMIUKUMUJJZUOUNUKUP
    UMUJKHZUOUMUJCLUQUJUMGHZUOUKUQURUOUMUJUMMNUKUJULGHULUMGHZURUJCOUKULCDZUSUJP
    ZULCOQUJULUMRSTUAUMAUBUCUDUNUKUNUMCDZUKUMAUEUKUTVBVAULPUFUGUHUI $.
    $( [27-Jun-2008] $)

  $( No set equals its power set. $)
  pwne $p |- ( A e. B -> ~P A =/= A ) $=
    ( wcel cpw wceq wn wne csdm wbr sdomirr breq1 canth2g syl5cbir mtoi df-ne
    sylibr ) ABCZADZAEZFRAGQSRRHIZRJSTARHIQRARHKABLMNRAOP $.
    $( [17-Nov-2008] $)

  $( No set equals the power set of its power set. $)
  2pwne $p |- ( A e. B -> ~P ~P A =/= A ) $=
    ( wcel cpw wceq wn wne csdm wbr sdomirr breq1 canth2g cvv pwexg syl sdomtr
    syl11anc syl5cbir mtoi df-ne sylibr ) ABCZADZDZAEZFUDAGUBUEUDUDHIZUDJUEUFAU
    DHIZUBUDAUDHKUBAUCHIUCUDHIZUGABLUBUCMCUHABNUCMLOAUCUDPQRSUDATUA $.
    $( [17-Nov-2008] $)

