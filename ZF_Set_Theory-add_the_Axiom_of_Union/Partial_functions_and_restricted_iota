$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Partial functions and restricted iota
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Undef $.
  $c iota_ $.

  $( Extend class notation with undefined value function. $)
  cund $a class Undef $.

  $( Extend class notation with restricted description binder. $)
  crio $a class ( iota_ x e. A ph ) $.

  $( Def. 8.3.36 p. 181. $)
  $( Define the undefined value function, whose value at set ` s ` is
     guaranteed not to be a member of ` s ` (see ~ pwuninel ). $)
  df-undef $a |- Undef = ( s e. _V |-> ~P U. s ) $.

  ${
    $d s S $.
    undefval.1 $e |- S e. _V $.
    $( Value of the undefined value function.  Normally we will not reference
       the explicit value but will use ~ undefnel instead. $)
    undefval $p |- ( Undef ` S ) = ~P U. S $=
      ( vs cvv wcel cund cfv cuni cpw wceq cv unieq pweq syl df-undef uniex
      pwex fvmpt ax-mp ) ADEAFGAHZIZJBCACKZHZIZUADFUBAJUCTJUDUAJUBALUCTMNCOTABP
      QRS $.
      $( [24-Sep-2011] $) $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel2 $p |- -. ( Undef ` S ) e. S $=
      ( cund cfv wcel cuni cpw pwuninel undefval eleq1i mtbir ) ACDZAEAFGZAEAHL
      MAABIJK $.
      $( [24-Sep-2011] $) $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel $p |- ( Undef ` S ) e/ S $=
      ( cund cfv wnel wcel wn undefnel2 df-nel mpbir ) ACDZAEKAFGABHKAIJ $.
      $( [24-Sep-2011] $) $( [15-Sep-2011] $)
  $}

  ${
    $( Define restricted description binder.  In case it doesn't exist, we
       return a set which is not a member of the domain of discourse ` A ` . $)
    df-riota $a |- ( iota_ x e. A ph ) =
       if ( E! x e. A ph , ( iota x ( x e. A /\ ph ) ) , ( Undef ` A ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqdv.1 $e |- ( ph -> A = B ) $.
    $( Formula-building deduction rule for iota. $)
    riotaeqdv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ps ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio wceq wb reueq1 syl eleq2d anbi1d
      iotabidv fveq2d ifbieq12d df-riota 3eqtr4g ) ABCDGZCHZDIZBJZCKZDLMZNBCEGZ
      UHEIZBJZCKZELMZNBCDOBCEOAUGUMUKULUPUQADEPUGUMQFBCDERSAUJUOCAUIUNBADEUHFTU
      AUBADELFUCUDBCDUEBCEUEUF $.
      $( [23-Sep-2011] $) $( [15-Sep-2011] $)
  $}

  ${
    $d x ph $.
    riotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for iota. $)
    riotabidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio reubidv anbi2d iotabidv eqidd
      ifbieq12d df-riota 3eqtr4g ) ABDEGZDHEIZBJZDKZELMZNCDEGZUDCJZDKZUGNBDEOCD
      EOAUCUHUFUGUJUGABCDEFPAUEUIDABCUDFQRAUGSTBDEUACDEUAUB $.
      $( [23-Sep-2011] $) $( [15-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqbidv.1 $e |- ( ph -> A = B ) $.
    riotaeqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    riotaeqbidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ch ) ) $=
      ( crio riotabidv riotaeqdv eqtrd ) ABDEICDEICDFIABCDEHJACDEFGKL $.
      $( [23-Sep-2011] $) $( [15-Sep-2011] $)
  $}

  $( Restricted iota is a set. $)
  riotaex $p |- ( iota_ x e. A ps ) e. _V $=
    ( crio wreu cv wcel wa cio cund cfv cif cvv df-riota iotaex fvex ifex
    eqeltri ) ABCDABCEZBFCGAHZBIZCJKZLMABCNSUAUBTBOCJPQR $.
    $( [23-Sep-2011] $) $( [15-Sep-2011] $)

  $( An iota restricted to the universe is unrestricted. $)
  riotav $p |- ( iota_ x e. _V ph ) = ( iota x ph ) $=
    ( cvv crio wreu cv wcel wa cio cund cfv cif df-riota wceq iftrue wb iotabi
    visset biantrur mpg syl6reqr wn c0 weu reuv notbii iotanul sylbi vprc
    fvprc ax-mp syl6eqr iffalse eqtr4d pm2.61i eqtr4i ) ABCDABCEZBFCGZAHZBIZCJK
    ZLZABIZABCMUQVCVBNUQVBUTVCUQUTVAOAUSPVCUTNBAUSBQURABRSTUAUQUBZVCVAVBVDVCUCV
    AVDABUDZUBVCUCNUQVEABUEUFABUGUHCCGUBVAUCNUICJUJUKULUQUTVAUMUNUOUP $.
    $( [23-Sep-2011] $) $( [18-Sep-2011] $)

  $( Restricted iota in terms of iota. $)
  riotaiota $p |- ( E! x e. A ph
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( wreu cv wcel wa cio cund cfv cif crio iftrue df-riota syl5eq ) ABCDZPBECF
    AGBHZCIJZKQABCLPQRMABCNO $.
    $( [23-Sep-2011] $) $( [15-Sep-2011] $)

  $( For proper classes, restricted and unrestricted iota are the same. $)
  riotaprc $p |- ( -. A e. _V
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( cvv wcel wn wreu crio cv wa cio wceq cund cfv c0 fvprc adantr cif
    iffalse adantl df-riota syl5eq weu df-reu notbii iotanul sylbi 3eqtr4d ex
    riotaiota pm2.61d2 ) CDEFZABCGZABCHZBICEAJZBKZLZULUMFZUQULURJZCMNZOUNUPULUT
    OLURCMPQUSUMUPUTRZUTUNURVAUTLULUMUPUTSTABCUAUBURUPOLZULURUOBUCZFVBUMVCABCUD
    UEUOBUFUGTUHUIABCUJUK $.
    $( [23-Sep-2011] $) $( [15-Sep-2011] $)

  $( Restricted iota in terms of class union. $)
  riotauni $p |- ( E! x e. A ph
                    -> ( iota_ x e. A ph ) = U. { x e. A | ph } ) $=
    ( wreu crio cv wcel wa cio crab cuni riotaiota cab weu wceq df-reu
    iotaequ sylbi df-rab unieqi syl6eqr eqtrd ) ABCDZABCEBFCGAHZBIZABCJZKZABC
    LUCUEUDBMZKZUGUCUDBNUEUIOABCPUDBQRUFUHABCSTUAUB $.
    $( [11-Oct-2011] $)

  ${
    $d x y A $.  $d y ph $.
    $( The abstraction variable in a restricted iota descriptor isn't free. $)
    hbriota1 $p |- ( y e. ( iota_ x e. A ph )
                       -> A. x y e. ( iota_ x e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hbreu1 hbiota1 ax-17
      hbfv hbif hbxfr ) BCABDEABDFZBGDHAIZBJZDKLZMABDNUABCUCUDABDOUBBCPBCDKCGZK
      HBQUEDHBQRST $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.
    hbriota.1 $e |- ( ph -> A. x ph ) $.
    hbriota.2 $e |- ( z e. A -> A. x z e. A ) $.
    $( A variable not free in a wff remains so in a restricted iota
       descriptor. $)
    hbriota $p |- ( z e. ( iota_ y e. A ph )
                       -> A. x z e. ( iota_ y e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hblem hbreu hban hbiota
      ax-17 hbfv hbif hbxfr ) BDACEHACEIZCJEKZALZCMZENOZPACEQUFBDUIUJABCEBDCEGR
      ZFSUHBCDUGABUKFTUABDENDJNKBUBGUCUDUE $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x A $.
    riotaclb.1 $e |- A e. _V $.
    $( Closure of restricted iota. $)
    riotaclb $p |- ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio wcel cv wa cio riotaiota reiotacl eqeltrd wn cund cfv
      undefnel2 cif iffalse df-riota syl5eq eleq1d mtbiri con4i impbii ) ABCEZA
      BCFZCGZUFUGBHCGAIBJZCABCKABCLMUFUHUFNZUHCOPZCGCDQUJUGUKCUJUFUIUKRUKUGUFUI
      UKSABCTUAUBUCUDUE $.
      $( [23-Sep-2011] $) $( [15-Sep-2011] $)

    $( Restricted iota equals the undefined value of its domain of discourse
       ` A ` when not meaningful. $)
    riotaund $p |- ( -. E! x e. A ph
                      <-> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
      ( wreu wn crio cund cfv wceq cv wcel wa cio cif iffalse df-riota syl5eq
      undefnel2 eleq1 riotaclb syl5bb mtbiri impbii ) ABCEZFZABCGZCHIZJZUFUEBKC
      LAMBNZUHOUHUGUEUJUHPABCQRUIUEUHCLZCDSUIUGCLUKUEUGUHCTABCDUAUBUCUD $.
      $( [26-Sep-2011] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  riota4 $p |- ( E! x e. A ph -> [ ( iota_ x e. A ph ) / x ] ph ) $=
    ( wreu crio wsbc cv wcel wa cio reiota4 wceq wb riotaiota dfsbcq syl
    mpbird ) ABCDZABABCEZFZABBGCHAIBJZFZABCKRSUALTUBMABCNABSUAOPQ $.
    $( [23-Sep-2011] $) $( [16-Sep-2011] $)

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    riota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( This theorem shows a condition that allows us to represent a descriptor
       with a class expression ` B ` .  The second hypothesis is a weakened
       bound variable condition that allows ~ hbsbc1g to be used.  Compare
       ~ reuuni2f . $)
    riota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota_ x e. A ph ) = B ) ) $=
      ( wcel wreu wa cv cio wceq crio reiota2f wb riotaiota eqeq1d adantl
      bitr4d ) FEJZACEKZLBCMEJALCNZFOZACEPZFOZABCDEFGHIQUDUHUFRUCUDUGUEFACESTUA
      UB $.
      $( [11-Oct-2011] $)
  $}


