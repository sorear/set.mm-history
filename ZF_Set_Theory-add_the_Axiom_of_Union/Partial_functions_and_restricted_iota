$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    Partial functions and restricted iota
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Undef $.
  $c iota_ $.

  $( Extend class notation with undefined value function. $)
  cund $a class Undef $.

  $( Extend class notation with restricted description binder. $)
  crio $a class ( iota_ x e. A ph ) $.

  $( Def. 8.3.36 p. 181. $)
  $( Define the undefined value function, whose value at set ` s ` is
     guaranteed not to be a member of ` s ` (see ~ pwuninel ). $)
  df-undef $a |- Undef = ( s e. _V |-> ~P U. s ) $.

  ${
    $d s S $.
    undefval.1 $e |- S e. _V $.
    $( Value of the undefined value function.  Normally we will not reference
       the explicit value but will use ~ undefnel instead. $)
    undefval $p |- ( Undef ` S ) = ~P U. S $=
      ( vs cvv wcel cund cfv cuni cpw wceq cv unieq pweq syl df-undef uniex
      pwex fvmpt ax-mp ) ADEAFGAHZIZJBCACKZHZIZUADFUBAJUCTJUDUAJUBALUCTMNCOTABP
      QRS $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel2 $p |- -. ( Undef ` S ) e. S $=
      ( cund cfv wcel cuni cpw pwuninel undefval eleq1i mtbir ) ACDZAEAFGZAEAHL
      MAABIJK $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel $p |- ( Undef ` S ) e/ S $=
      ( cund cfv wnel wcel wn undefnel2 df-nel mpbir ) ACDZAEKAFGABHKAIJ $.
      $( [15-Sep-2011] $)
  $}

  ${
    $( Define restricted description binder.  In case it doesn't exist, we
       return a set which is not a member of the domain of discourse ` A ` . $)
    df-riota $a |- ( iota_ x e. A ph ) =
       if ( E! x e. A ph , ( iota x ( x e. A /\ ph ) ) , ( Undef ` A ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqdv.1 $e |- ( ph -> A = B ) $.
    $( Formula-building deduction rule for iota. $)
    riotaeqdv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ps ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio wceq wb reueq1 syl eleq2d anbi1d
      iotabidv fveq2d ifbieq12d df-riota 3eqtr4g ) ABCDGZCHZDIZBJZCKZDLMZNBCEGZ
      UHEIZBJZCKZELMZNBCDOBCEOAUGUMUKULUPUQADEPUGUMQFBCDERSAUJUOCAUIUNBADEUHFTU
      AUBADELFUCUDBCDUEBCEUEUF $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x ph $.
    riotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for restricted iota. $)
    riotabidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio reubidv anbi2d iotabidv eqidd
      ifbieq12d df-riota 3eqtr4g ) ABDEGZDHEIZBJZDKZELMZNCDEGZUDCJZDKZUGNBDEOCD
      EOAUCUHUFUGUJUGABCDEFPAUEUIDABCUDFQRAUGSTBDEUACDEUAUB $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqbidv.1 $e |- ( ph -> A = B ) $.
    riotaeqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    riotaeqbidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ch ) ) $=
      ( crio riotabidv riotaeqdv eqtrd ) ABDEICDEICDFIABCDEHJACDEFGKL $.
      $( [15-Sep-2011] $)
  $}

  $( Restricted iota is a set. $)
  riotaex $p |- ( iota_ x e. A ps ) e. _V $=
    ( crio wreu cv wcel wa cio cund cfv cif cvv df-riota iotaex fvex ifex
    eqeltri ) ABCDABCEZBFCGAHZBIZCJKZLMABCNSUAUBTBOCJPQR $.
    $( [15-Sep-2011] $)

  $( An iota restricted to the universe is unrestricted. $)
  riotav $p |- ( iota_ x e. _V ph ) = ( iota x ph ) $=
    ( cvv crio wreu cv wcel wa cio cund cfv cif df-riota wceq iftrue wb iotabi
    visset biantrur mpg syl6reqr wn c0 weu reuv notbii iotanul sylbi vprc
    fvprc ax-mp syl6eqr iffalse eqtr4d pm2.61i eqtr4i ) ABCDABCEZBFCGZAHZBIZCJK
    ZLZABIZABCMUQVCVBNUQVBUTVCUQUTVAOAUSPVCUTNBAUSBQURABRSTUAUQUBZVCVAVBVDVCUCV
    AVDABUDZUBVCUCNUQVEABUEUFABUGUHCCGUBVAUCNUICJUJUKULUQUTVAUMUNUOUP $.
    $( [18-Sep-2011] $)

  $( Restricted iota in terms of iota. $)
  riotaiota $p |- ( E! x e. A ph
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( wreu crio cv wcel wa cio cund cfv cif df-riota iftrue syl5eq ) ABCDZABCEP
    BFCGAHBIZCJKZLQABCMPQRNO $.
    $( [15-Sep-2011] $)

  $( For proper classes, restricted and unrestricted iota are the same. $)
  riotaprc $p |- ( -. A e. _V
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( cvv wcel wn wreu crio cv wa cio wceq cund cfv c0 fvprc adantr cif
    df-riota iffalse adantl syl5eq weu df-reu notbii iotanul sylbi 3eqtr4d ex
    riotaiota pm2.61d2 ) CDEFZABCGZABCHZBICEAJZBKZLZULUMFZUQULURJZCMNZOUNUPULUT
    OLURCMPQUSUNUMUPUTRZUTABCSURVAUTLULUMUPUTTUAUBURUPOLZULURUOBUCZFVBUMVCABCUD
    UEUOBUFUGUAUHUIABCUJUK $.
    $( [15-Sep-2011] $)

  $( Restricted iota in terms of class union. $)
  riotauni $p |- ( E! x e. A ph
                    -> ( iota_ x e. A ph ) = U. { x e. A | ph } ) $=
    ( wreu crio cv wcel wa cio crab cuni riotaiota cab weu wceq df-reu iotaequ
    sylbi df-rab unieqi syl6eqr eqtrd ) ABCDZABCEBFCGAHZBIZABCJZKZABCLUCUEUDBMZ
    KZUGUCUDBNUEUIOABCPUDBQRUFUHABCSTUAUB $.
    $( [11-Oct-2011] $)

  ${
    $d x y A $.  $d y ph $.
    $( The abstraction variable in a restricted iota descriptor isn't free. $)
    hbriota1 $p |- ( y e. ( iota_ x e. A ph )
                       -> A. x y e. ( iota_ x e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hbreu1 hbiota1 ax-17
      hbfv hbif hbxfr ) BCABDEABDFZBGDHAIZBJZDKLZMABDNUABCUCUDABDOUBBCPBCDKCGZK
      HBQUEDHBQRST $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.
    hbriota.1 $e |- ( ph -> A. x ph ) $.
    hbriota.2 $e |- ( z e. A -> A. x z e. A ) $.
    $( A variable not free in a wff remains so in a restricted iota
       descriptor. $)
    hbriota $p |- ( z e. ( iota_ y e. A ph )
                       -> A. x z e. ( iota_ y e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hblem hbreu hban hbiota
      ax-17 hbfv hbif hbxfr ) BDACEHACEIZCJEKZALZCMZENOZPACEQUFBDUIUJABCEBDCEGR
      ZFSUHBCDUGABUKFTUABDENDJNKBUBGUCUDUE $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.  $d z ps $.
    hbriotad.1 $e |- ( ph -> A. x ph ) $.
    hbriotad.2 $e |- ( ph -> A. y ph ) $.
    hbriotad.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbriotad.4 $e |- ( ph -> ( z e. A -> A. x z e. A ) ) $.
    $( Deduction version of ~ hbriota . $)
    hbriotad $p |- ( ph -> ( z e. ( iota_ y e. A ps )
                       -> A. x z e. ( iota_ y e. A ps ) ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif wceq df-riota a1i ax-17 hblemd
      hbreud hband hbiotad wal wi hbfvd hbifd hbxfrd ) ACEBDFKZBDFLZDMFNZBOZDPZ
      FQRZSZGUMUSTABDFUAUBAUNCEUQURGABCDFGHACEDFGAEUCJUDZIUEAUPCDEGHAUOBCUTIUFU
      GACEFQGEMQNZVACUHUIAVACUCUBJUJUKUL $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d y z A $.  $d x z w B $.  $d w z ph $.  $d w x y $.
    $( Distribute proper substitution through a restricted description
       binder. $)
    csbriota2g $p |- ( A e. V
      -> [_ A / x ]_ ( iota_ y e. B ph ) = ( iota_ y e. B [ A / x ] ph ) ) $=
      ( vz vw cv crio csb wsbc wceq csbeq1 dfsbcq riotabidv eqeq12d visset
      hbs1 wcel ax-17 hbriota sbceq1a csbief vtoclg ) BGIZACEJZKZABUFLZCEJZMBDU
      GKZABDLZCEJZMGDFUFDMZUHUKUJUMBUFDUGNUNUIULCEABUFDOPQBHUFUGUJGRUIBCHEABGSH
      IETBUAUBBIUFMAUICEABUFUCPUDUE $.
      $( [24-Feb-2013] $)
  $}

  ${
    $d x A $.
    $( Closure of restricted iota. $)
    riotacl $p |- ( E! x e. A ph -> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio cv wcel wa cio riotaiota reiotacl eqeltrd ) ABCDABCEBFCGAHBIC
      ABCJABCKL $.
      $( [16-Jan-2012] $)
  $}

  $( Restricted iota equals the undefined value of its domain of discourse
     ` A ` when not meaningful. $)
  riotaund $p |- ( -. E! x e. A ph
                    -> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
    ( wreu wn crio cv wcel wa cio cund cfv cif df-riota iffalse syl5eq ) ABCDZE
    ABCFQBGCHAIBJZCKLZMSABCNQRSOP $.
    $( [16-Jan-2012] $)

  ${
    $d x A $.
    riotaclb.1 $e |- A e. _V $.
    $( Closure of restricted iota. $)
    riotaclb $p |- ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio wcel riotacl wn cund cfv undefnel2 cv wa cio cif df-riota
      iffalse syl5eq eleq1d mtbiri con4i impbii ) ABCEZABCFZCGZABCHUDUFUDIZUFCJ
      KZCGCDLUGUEUHCUGUEUDBMCGANBOZUHPUHABCQUDUIUHRSTUAUBUC $.
      $( [15-Sep-2011] $)

    $( Restricted iota equals the undefined value of its domain of discourse
       ` A ` when not meaningful. $)
    riotaundb $p |- ( -. E! x e. A ph
                      <-> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
      ( wreu wn crio cund cfv wceq riotaund wcel undefnel2 riotaclb eleq1
      syl5bb mtbiri impbii ) ABCEZFABCGZCHIZJZABCKUBSUACLZCDMSTCLUBUCABCDNTUACO
      PQR $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d x ph $.
    riotabidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (deduction
       rule).  (Th. ~ rabbidva analog.) $)
    riotabidva $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu crio wceq wa cv wcel cio pm5.32da iotabidv adantr riotaiota
      adantl reubidva syl6bi imp 3eqtr4d wn cund cfv riotaund notbid eqtr4d
      pm2.61dan ) ABDEGZBDEHZCDEHZIAUJJDKELZBJZDMZUMCJZDMZUKULAUOUQIUJAUNUPDAUM
      BCFNOPUJUKUOIABDEQRAUJULUQIZAUJCDEGZURABCDEFSZCDEQTUAUBAUJUCZJUKEUDUEZULV
      AUKVBIABDEUFRAVAULVBIZAVAUSUCVCAUJUSUTUGCDEUFTUAUHUI $.
      $( [17-Jan-2012] $)
  $}

  ${
    riotabiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Equivalent wff's yield equal restricted iotas (inference rule).  (Th.
       ~ rabbiia analog.) $)
    riotabiia $p |- ( iota_ x e. A ph ) = ( iota_ x e. A ps ) $=
      ( cvv wceq crio eqid cv wcel wb adantl riotabidva ax-mp ) FFGZACDHBCDHGFI
      PABCDCJDKABLPEMNO $.
      $( [16-Jan-2012] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  riota4 $p |- ( E! x e. A ph -> [ ( iota_ x e. A ph ) / x ] ph ) $=
    ( wreu crio wsbc cv wcel wa cio reiota4 wceq wb riotaiota dfsbcq syl
    mpbird ) ABCDZABABCEZFZABBGCHAIBJZFZABCKRSUALTUBMABCNABSUAOPQ $.
    $( [16-Sep-2011] $)

  ${
    $d x y A $.
    $( Membership law for "the unique element in ` A ` such that ` ph ` ."
       (Th. ~ reucl2 analog.) $)
    riotacl2 $p |- ( E! x e. A ph
            -> ( iota_ x e. A ph ) e. { x e. A | ph } ) $=
      ( vy wreu crio wcel wsbc crab riotacl riota4 cv ax-17 elrabsf sylanbrc )
      ABCEABCFZCGABPHPABCIGABCJABCKABDPCDLCGBMNO $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d y ph $.  $d y ps $.  $d x y A $.  $d y B $.
    riota2df.1 $e |- ( ph -> A. x ph ) $.
    riota2df.2 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    riota2df.3 $e |- ( ( ph /\ B e. A ) -> ( ch -> A. x ch ) ) $.
    riota2df.4 $e |- ( ph -> ( x = B -> ( ps <-> ch ) ) ) $.
    $( A deduction version of ~ riota2f . $)
    riota2df $p |- ( ( ph /\ B e. A /\ E! x e. A ps ) ->
            ( ch <-> ( iota_ x e. A ps ) = B ) ) $=
      ( wcel wreu w3a cv wa cio wceq crio reiota2df wb riotaiota eqeq1d
      3ad2ant3 bitr4d ) AGFLZBDFMZNCDOFLBPDQZGRZBDFSZGRZABCDEFGHIJKTUGAUKUIUAUF
      UGUJUHGBDFUBUCUDUE $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    riota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( This theorem shows a condition that allows us to represent a descriptor
       with a class expression ` B ` .  The second hypothesis is a weakened
       bound variable condition that allows ~ hbsbc1g to be used.  Compare
       ~ reuuni2f . $)
    riota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota_ x e. A ph ) = B ) ) $=
      ( wcel wreu wa cv cio wceq crio reiota2f wb riotaiota eqeq1d adantl
      bitr4d ) FEJZACEKZLBCMEJALCNZFOZACEPZFOZABCDEFGHIQUDUHUFRUCUDUGUEFACESTUA
      UB $.
      $( [11-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ph $.  $d y ps $.
    riota5.1 $e |- ( ( ph /\ B e. A /\ x e. A ) -> ( ps <-> x = B ) ) $.
    $( A method for computing restricted iota. $)
    riota5 $p |- ( ( ph /\ B e. A ) -> ( iota_ x e. A ps ) = B ) $=
      ( vy wcel wa wsbc crio wceq cv wex elex adantl wi wal simpr simpll
      simplr eleq1a imp adantll w3a biimparc syl13anc wb sbceq1a mpbid ex
      19.21aiv ax-17 hbsbc1g 19.23t syl mpd wreu wral wrex 3expa r19.21aiva
      eqeq2 bibi2d ralbidv rcla4ev syl11anc reu6 sylibr riota2f ) AEDHZIZBCEJZB
      CDKELZVLCMZELZCNZVMVKVQACEDOPVLVPVMQZCRZVQVMQZVLVRCVLVPVMVLVPIZBVMWAVPAVK
      VODHZBVLVPSAVKVPTAVKVPUAVKVPWBAVKVPWBEDVOUBUCUDAVKWBUEBVPFUFUGVPBVMUHVLBC
      EUIZPUJUKULVLVMVMCRQZCRZVSVTUHVKWEAVKWDCBCGEDGMZEHCUMZUNZULPVPVMCUOUPUJUQ
      VLVKBCDURZVMVNUHAVKSZVLBVOWFLZUHZCDUSZGDUTZWIVLVKBVPUHZCDUSZWNWJVLWOCDAVK
      WBWOFVAVBWMWPGEDWFELZWLWOCDWQWKVPBWFEVOVCVDVEVFVGBCGDVHVIBVMCGDEWGWHWCVJV
      GUJ $.
      $( [20-Oct-2011] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d x y ph $.  $d ps y z $.
    $d ch x z $.
    riotaxfrd.1 $e |- ( z e. C -> A. y z e. C ) $.
    riotaxfrd.2 $e |- ( ( ph /\ y e. A ) -> B e. A ) $.
    riotaxfrd.3 $e |- ( ( ph /\ ( iota_ y e. A ch ) e. A ) -> C e. A ) $.
    riotaxfrd.4 $e |- ( x = B -> ( ps <-> ch ) ) $.
    riotaxfrd.5 $e |- ( y = ( iota_ y e. A ch ) -> B = C ) $.
    riotaxfrd.6 $e |- ( ( ph /\ x e. A ) -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` x ` such
       that ` ps ` " to another variable ` y ` contained in expression ` B ` .
       Use ~ reuhypd to eliminate the last hypothesis.  (Th. ~ reuunixfr
       analog.) $)
    riotaxfrd $p |- ( ( ph /\ E! x e. A ps ) -> ( iota_ x e. A ps ) = C ) $=
      ( wreu wa crio cv crab wcel rabid baib riotabiia wceq reuxfrd riotacl2
      adantl wb riotacl hbriota1 rabxfrd sylan2 mpbird ex sylbid imp syl5
      baibr reubiia biimpi ax-17 wal wi hbrab1 hbel a1i eleq1 riota2f syl11anc
      mpbid syl5eqr ) ABDGPZQZBDGRDSZBDGTZUAZDGRZIVQBDGVQVOGUAZBBDGUBZUCUDVNIVP
      UAZVRIUEZAVMWAAVMCEGPZWAABCDEHGKOMUFZAWCWAAWCQWACEGRZCEGTUAZWCWFACEGUGUHW
      CAWEGUAZWAWFUICEGUJZABCDEFHWEIGCEFGUKJKMNULUMUNUOUPUQVNIGUAZVQDGPZWAWBUIA
      VMWIAVMWCWIWDWCWGAWIWHAWGWILUOURUPUQVMWJAVMWJBVQDGVQVSBVTUSUTVAUHVQWADFGI
      FSIUADVBZWAWADVCVDWIDFFIVPWKBDFGVEVFVGVOIVPVHVIVJVKVL $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasvd.1 $e |- A e. _V $.
    riotasvd.2 $e |- ( ph -> A. x ph ) $.
    riotasvd.3 $e |- ( ph -> A. y ph ) $.
    riotasvd.4 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    $( Deduction version of ~ riotasv . $)
    riotasvd $p |- ( ( ph /\ D e. A /\ ( y e. B /\ ps ) ) -> D = C ) $=
      ( vz wcel cv wa wceq wi wral crio eqcomd adantr wreu wb eleq1d riotaclb
      biimpri syl6bi imp wal hbriota1 a1i hbxfrd ax-17 hbeqd hbimd hbrald a1d
      hbra1 hbriotad hband anabsi5 eqeq1 adantl imbi2d ralbid ex riota2df
      mpd3an3 mpbird ra4 syl6 imp4a 3imp ) AHENZDOFNZBPHGQZAVOVPBVQAVOBVQRZDFSZ
      VPVRRAVOVSAVOPVSBCOZGQZRZDFSZCETZHQZAWEVOAHWDLUAUBAVOWCCEUCZVSWEUDAVOWFAV
      OWDENZWFAHWDELUEWFWGWCCEIUFUGUHUIAWCVSCMEHJACMHWDJLMOZWDNZWICUJRAWCCMEUKU
      LUMZAVSVSCUJRVOAVRCDFJKVPVPCUJRAVPCUNULABVQCJBBCUJRABCUNULACMMHGJWJWHGNZW
      KCUJRAWKCUNULUOUPUQUBAVTHQZWCVSUDAWLPZWBVRDFAWLWMDUJAAWLDAADUJAKURADMMVTH
      KWHVTNZWNDUJRAWNDUNULADMHWDKLAWCDCMEKJWCWCDUJRAWBDFUSULWHENZWODUJRAWODUNU
      LUTUMUOVAVBWMWAVQBWLWAVQUDAVTHGVCVDVEVFVGVHVIVJVGVRDFVKVLVMVN $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d x z ph $.
    riotasv.1 $e |- A e. _V $.
    riotasv.2 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    $( Value of the description binder ` D ` for a single-valued class
       expression ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of
       ~ riota2f . $)
    riotasv $p |- ( ( D e. A /\ y e. B /\ ph ) -> D = C ) $=
      ( vz wcel cv wceq wi wral crio eqcomi wreu wb eleq1i riotaclb biimpri
      sylbi hbriota1 hbxfr wal ax-17 hbeq hbim hbral a1i hbra1 hbriota eqeq1
      imbi2d ralbid riota2f mpdan mpbiri ra4 syl 3imp ) GDKZCLEKZAGFMZVCAVENZCE
      OZVDVFNVCVGABLZFMZNZCEOZBDPZGMZGVLIQVCVKBDRZVGVMSVCVLDKZVNGVLDITVNVOVKBDH
      UAUBUCVKVGBJDGBJGVLIVKBJDUDUEZVGVGBUFNVCVFBCEVDBUGAVEBABUGBJJGFVPJLZFKBUG
      UHUIUJUKVHGMZVJVFCECJJVHGVQVHKCUGCJGVLIVKCBJDVJCEULVQDKCUGUMUEUHVRVIVEAVH
      GFUNUOUPUQURUSVFCEUTVAVB $.
      $( [26-Jan-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d x z ph $.
    riotasv3.1 $e |- A e. _V $.
    riotasv3.2 $e |- ( ps -> A. y ps ) $.
    riotasv3.3 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    riotasv3.4 $e |- ( ( y e. B /\ ph ) -> ch ) $.
    riotasv3.5 $e |- ( D = C -> ( ps <-> ch ) ) $.
    $( A property ` ch ` holding for a representative of a single-valued class
       expression ` C ( y ) ` (see e.g. ~ reusv2 ) also holds for its
       description binder ` D ` (in the form of property ` ps ` ). $)
    riotasv3 $p |- ( ( D e. A /\ E. y e. B ph ) -> ps ) $=
      ( vz wcel wrex cv wceq wi wral crio hbra1 ax-17 hbriota hbxfr hbel w3a
      3adant1 wb riotasv syl mpbird 3exp r19.23ad imp ) IFPZAEGQBUQABEGEOOIFEOI
      ADRHSTZEGUAZDFUBLUSEDOFUREGUCORFPEUDZUEUFUTUGKUQERGPZABUQVAAUHZBCVAACUQMU
      IVBIHSBCUJADEFGHIJLUKNULUMUNUOUP $.
      $( [26-Jan-2013] $)
  $}


