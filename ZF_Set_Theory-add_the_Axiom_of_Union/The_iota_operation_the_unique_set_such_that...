$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The iota operation ("the unique set such that...")
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c iota $.
  $( Extend class notation with Russell's definition description binder. $)
  cio $a class ( iota x ph ) $.

  ${
    $d w x z $.  $d ph w z $.  $d ph w y $.  $d x y $.
    $( Soundness justification theorem for ~ df-iota .  (Contributed by Andrew
       Salmon, 29-Jun-2011.) $)
    iotajust $p |- U. { y | { x | ph } = { y } } =
       U. { z | { x | ph } = { z } } $=
      ( vw cab cv csn wceq sneq eqeq2d cbvabv eqtri unieqi ) ABFZCGZHZIZCFZODGZ
      HZIZDFZSOEGZHZIZEFUCRUFCEPUDIQUEOPUDJKLUFUBEDUDTIUEUAOUDTJKLMN $.
      $( [12-Jul-2011] $) $( [29-Jun-2011] $)
  $}

  ${
    $d y x $.  $d y ph $.
    $( Define Russell's definition description binder, which can be read as
       "the unique ` x ` such that ` ph ` ," where ` ph ` ordinarily contains
       ` x ` as a free variable.  Our definition is meaningful only when there
       is exactly one ` x ` such that ` ph ` is true (see ~ iotaval );
       otherwise, it evaluates to the empty set (see ~ iotanul ). $)
    df-iota $a |- ( iota x ph ) = U. { y | { x | ph } = { y } } $.
  $}

  ${
    $d y x $.  $d y ph $.
    $( Alternate definition for descriptions.  Definition 8.18 in [Quine]
       p. 56.  (Contributed by Andrew Salmon, 30-Jun-2011.) $)
    dfiota2 $p |- ( iota x ph ) = U. { y | A. x ( ph <-> x = y ) } $=
      ( cio cab cv csn wceq cuni wb wal df-iota df-sn eqeq2i eq2ab bitri abbii
      unieqi eqtri ) ABDABEZCFZGZHZCEZIABFUAHZJBKZCEZIABCLUDUGUCUFCUCTUEBEZHUFU
      BUHTBUAMNAUEBOPQRS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( Bound-variable hypothesis builder for the ` iota ` class.  (Contributed
       by Andrew Salmon, 11-Jul-2011.) $)
    hbiota1 $p |- ( y e. ( iota x ph ) -> A. x y e. ( iota x ph ) ) $=
      ( vz cio cab cv csn wceq cuni df-iota wcel wa wex wal ax-17 hbab1 hbeq
      hban hbex eluniab albii 3imtr4i hbxfr ) BCABEABFZDGZHZIZDFJZABDKCGZUFLZUH
      MZDNZUMBOUJUILZUNBOULBDUKUHBUKBPBDCUEUGABDQUJUGLBPRSTUHDUJUAZUNUMBUOUBUCU
      D $.
      $( [6-Sep-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d w z ph $.  $d w x z $.  $d w y z $.
    hbiota.1 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for the ` iota ` class. $)
    hbiota $p |- ( z e. ( iota y ph ) -> A. x z e. ( iota y ph ) ) $=
      ( vw cio cab cv csn wceq cuni df-iota hbab wcel ax-17 hbeq hbuni hbxfr )
      BDACGACHZFIJZKZFHZLACFMBDUCUBBFDBDDTUAABCDENDIUAOBPQNRS $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d w z ps $.  $d w z ph $.  $d w x z $.  $d w y z $.
    hbiotad.1 $e |- ( ph -> A. x ph ) $.
    hbiotad.2 $e |- ( ph -> A. y ph ) $.
    hbiotad.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction version of ~ hbiota . $)
    hbiotad $p |- ( ph ->
          ( z e. ( iota y ps ) -> A. x z e. ( iota y ps ) ) ) $=
      ( vw cio cab cv csn wceq cuni df-iota a1i wal ax-17 19.21ai hbabd wcel
      wi hbeqd hbunid hbxfrd ) ACEBDJZBDKZILMZNZIKZOZFUGULNABDIPQACEUKFAUJCIEAA
      IRCFAISTACEEUHUIFABCDEAADRCFGTHUAELUIUBZUMCRUCAUMCSQUDUAUEUF $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d A y z $.  $d ph z $.  $d x y z $.
    $( Distribute proper substitution through a description binder. $)
    csbiotag $p |- ( A e. V
        -> [_ A / x ]_ ( iota y ph ) = ( iota y [ A / x ] ph ) ) $=
      ( vz wcel cvv cio csb wsbc wceq elisset cab cv csn cuni df-iota csbeq2i
      csbunig csbabg sbceq1g eqeq1d bitrd abbidv eqtrd unieqd 3eqtrd syl6eqr
      syl ) DEGDHGZBDACIZJZABDKZCIZLDEMUKUMUNCNZFOPZLZFNZQZUOUKUMBDACNZUQLZFNZQ
      ZJBDVCJZQUTBDULVDHACFRSBDVCHTUKVEUSUKVEVBBDKZFNUSVBBFDHUAUKVFURFUKVFBDVAJ
      ZUQLURBDVAUQHUBUKVGUPUQABCDHUAUCUDUEUFUGUHUNCFRUIUJ $.
      $( [24-Feb-2013] $)
  $}

  ${
    $d y z $.  $d x z $.  $d ph z $.
    $( Equality theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotaeq $p |- ( A. x x = y -> ( iota x ph ) = ( iota y ph ) ) $=
      ( vz cv wceq wal cab csn cuni cio wsbc wcel drsb1 df-clab 3bitr4g eqrdv
      eqeq1d abbidv unieqd df-iota 3eqtr4g ) BECEFBGZABHZDEZIZFZDHZJACHZUFFZDHZ
      JABKACKUCUHUKUCUGUJDUCUDUIUFUCDUDUIUCABUELACUELUEUDMUEUIMABCDNADBOADCOPQR
      STABDUAACDUAUB $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    $( Equivalence theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotabi $p |- ( A. x ( ph <-> ps ) -> ( iota x ph ) = ( iota x ps ) ) $=
      ( vz wb wal cab cv csn wceq cuni cio eq2ab biimpri eqeq1d abbidv unieqd
      df-iota 3eqtr4g ) ABECFZACGZDHIZJZDGZKBCGZUBJZDGZKACLBCLTUDUGTUCUFDTUAUEU
      BUAUEJTABCMNOPQACDRBCDRS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)

    $( Part of Theorem 8.17 in [Quine] p. 56.  This theorem serves as a lemma
       for the fundamental property of iota.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    uniabio $p |- ( A. x ( ph <-> x = y ) -> U. { x | ph } = y ) $=
      ( cv wceq wb wal cab cuni csn eq2ab biimpri df-sn syl6eqr unieqd visset
      unisn syl6eq ) ABDCDZEZFBGZABHZISJZISUAUBUCUAUBTBHZUCUBUDEUAATBKLBSMNOSCP
      QR $.
      $( [12-Jul-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.19 in [Quine] p. 57.  This theorem is the fundamental property
       of iota.  (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    iotaval $p |- ( A. x ( ph <-> x = y ) -> ( iota x ph ) = y ) $=
      ( vz cv wceq wb wal cio cab cuni dfiota2 cvv wcel wa wi visset sbeqalb
      equcomi syl6 ax-mp ex equequ2 eqcoms bibi2d biimpd alimdv com12 impbid
      19.21aiv uniabio syl syl5eq ) ABEZCEZFZGZBHZABIAUNDEZFZGZBHZDJKZUOABDLURV
      BUSUOFZGZDHVCUOFURVEDURVBVDURVBVDUOMNZURVBOZVDPCQVFVGUOUSFVDABUOUSMRCDSTU
      AUBVDURVBVDUQVABVDUQVAVDUPUTAUPUTGUOUSCDBUCUDUEUFUGUHUIUJVBDCUKULUM $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Equivalence between two different forms of ` iota ` .  (Contributed by
       Andrew Salmon, 12-Jul-2011.) $)
    iotaequ $p |- ( E! x ph -> ( iota x ph ) = U. { x | ph } ) $=
      ( vz weu cv wceq wb wal wex cio cab cuni df-eu iotaval uniabio eqtr4d
      19.23aiv sylbi ) ABDABECEZFGBHZCIABJZABKLZFZABCMTUCCTUASUBABCNABCOPQR $.
      $( [20-Aug-2011] $) $( [12-Jul-2011] $)

    $( Theorem 8.22 in [Quine] p. 57.  This theorem is the result if there
       isn't exactly one ` x ` that satisfies ` ph ` .  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotanul $p |- ( -. E! x ph -> ( iota x ph ) = (/) ) $=
      ( vz weu wn cv wceq wb wal wex cio c0 df-eu notbii cab cuni dfiota2
      alnex ax-1 eqidd impbid1 con2bid alimi eq2ab sylibr dfnul2 syl6eqr
      sylbir unieqd uni0 syl6eq syl5eq sylbi ) ABDZEABFCFZGHBIZCJZEZABKZLGUNUQA
      BCMNURUSUPCOZPZLABCQURVALPLURUTLURUPEZCIZUTLGUPCRVCUTUOUOGZEZCOZLVCUPVEHZ
      CIUTVFGVBVGCVBVDUPVBVDVBVBVDSVBUOTUAUBUCUPVECUDUECUFUGUHUIUJUKULUM $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.23 in [Quine] p. 58.  This theorem proves the existence of the
       ` iota ` class under our definition.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    iotaex $p |- ( iota x ph ) e. _V $=
      ( vz weu cio cvv wcel cv wceq wb wal wex iotaval eqcomd eximi df-eu
      isset 3imtr4i wn c0 iotanul 0ex syl6eqel pm2.61i ) ABDZABEZFGZABHCHZIJBKZ
      CLUHUFIZCLUEUGUIUJCUIUFUHABCMNOABCPCUFQRUESUFTFABUAUBUCUD $.
      $( [11-Jul-2011] $)

    $( Theorem *14.22 in [WhiteheadRussell] p. 190.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    iota4 $p |- ( E! x ph -> [ ( iota x ph ) / x ] ph ) $=
      ( vz weu cv wceq wb wal wex cio wsbc df-eu wi bi2 alimi sb2 syl iotaval
      eqcomd dfsbcq mpbid 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABABJZKZABCLUGUICU
      GABUDKZUIUGUEAMZBHUJUFUKBAUENOABCPQUGUDUHFUJUIGUGUHUDABCRSABUDUHTQUAUBUC
      $.
      $( [12-Jul-2011] $)
  $}

  $( Theorem *14.23 in [WhiteheadRussell] p. 191.  (Contributed by Andrew
     Salmon, 12-Jul-2011.) $)
  iota4an $p |- ( E! x ( ph /\ ps )
            -> [ ( iota x ( ph /\ ps ) ) / x ] ph ) $=
    ( wa weu cio wsbc iota4 wi cvv wcel iotaex simpl sbcth ax-mp wb sbcimg
    mpbi syl ) ABDZCETCTCFZGZACUAGZTCHTAIZCUAGZUBUCIZUAJKZUETCLZUDCUAJABMNOUGUE
    UFPUHTACUAJQORS $.
    $( [7-Sep-2011] $) $( [12-Jul-2011] $)

  ${
    $d x ph $.
    iotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for iota. $)
    iotabidv $p |- ( ph -> ( iota x ps ) = ( iota x ch ) ) $=
      ( wb wal cio wceq 19.21aiv iotabi syl ) ABCFZDGBDHCDHIAMDEJBCDKL $.
      $( [20-Aug-2011] $)
  $}

  $( Membership law for descriptions.  (Contributed by Andrew Salmon,
     1-Aug-2011.) $)
  iotacl $p |- ( E! x ph -> ( iota x ph ) e. { x | ph } ) $=
    ( weu cio wsbc cab wcel iota4 iotaex elabs sylibr ) ABCABABDZELABFGABHABLAB
    IJK $.
    $( [1-Aug-2011] $)

  $( Property of iota.  Compare ~ euuni . $)
  iota1 $p |- ( E! x ph -> ( ph <-> ( iota x ph ) = x ) ) $=
    ( weu cab cuni cv wceq cio euuni iotaequ eqeq1d bitr4d ) ABCZAABDEZBFZGABHZ
    OGABIMPNOABJKL $.
    $( [23-Aug-2011] $)

  $( Membership law for descriptions.  Compare ~ reucl2 . $)
  reiotacl2 $p |- ( E! x e. A ph
                  -> ( iota x ( x e. A /\ ph ) ) e. { x e. A | ph } ) $=
    ( wreu cv wcel wa cio cab crab weu df-reu iotacl sylbi df-rab syl6eleqr )
    ABCDZBECFAGZBHZRBIZABCJQRBKSTFABCLRBMNABCOP $.
    $( [21-Aug-2011] $)

  ${
    $d x A $.
    $( Membership law for descriptions.  Compare ~ reucl . $)
    reiotacl $p |- ( E! x e. A ph
                    -> ( iota x ( x e. A /\ ph ) ) e. A ) $=
      ( wreu crab cv wcel wa cio wss ssrab2 a1i reiotacl2 sseldd ) ABCDZABCEZCB
      FCGAHBIPCJOABCKLABCMN $.
      $( [21-Aug-2011] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  reiota4 $p |- ( E! x e. A ph -> [ ( iota x ( x e. A /\ ph ) ) / x ] ph ) $=
    ( cv wcel wa weu cio wsbc wreu iota4an df-reu ancom eubii bitri wceq wb
    iotabi mpg dfsbcq ax-mp 3imtr4i ) ABDCEZFZBGZABUDBHZIZABCJZABUCAFZBHZIZAUCB
    KUHUIBGUEABCLUIUDBUCAMZNOUJUFPZUKUGQUIUDQUMBUIUDBRULSABUJUFTUAUB $.
    $( [23-Aug-2011] $)

  $( Property of iota.  Compare ~ reuuni1 . $)
  reiota1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
          ( ph <-> ( iota x ( x e. A /\ ph ) ) = x ) ) $=
    ( cv wcel wa wreu cio wceq ibar weu wb df-reu iota1 sylbi sylan9bb ) BDZCEZ
    ARAFZABCGZSBHQIZRAJTSBKSUALABCMSBNOP $.
    $( [23-Aug-2011] $)

  ${
    $d y ph $.  $d y ps $.  $d x y A $.  $d y B $.
    reiota2df.1 $e |- ( ph -> A. x ph ) $.
    reiota2df.2 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    reiota2df.3 $e |- ( ( ph /\ B e. A ) -> ( ch -> A. x ch ) ) $.
    reiota2df.4 $e |- ( ph -> ( x = B -> ( ps <-> ch ) ) ) $.
    $( Deduction version of ~ reiota2f . $)
    reiota2df $p |- ( ( ph /\ B e. A /\ E! x e. A ps ) ->
            ( ch <-> ( iota x ( x e. A /\ ps ) ) = B ) ) $=
      ( wcel wreu cv wa cio wceq wb wi wal ax-17 19.21ai adantr a1d a1i hbeld
      hband anabsi5 hbreu1 hbiota1 hbeqd hbbid hbimd eleq1 adantl imp eqeq2
      bibi12d imbi2d imbi12d ex reiota1 ax-gen simpr vtoclgft syl221anc
      pm2.43d 3imp ) AGFLZBDFMZCDNZFLZBOZDPZGQZRZAVIVJVPSZAVIVIVQSZAVIOZENZGLZW
      ADTSZETZDTZVRVRDTSZDTVKGQZVLVJBVNVKQZRZSZSZVRRZSZDTZWJDTZVIVRAWDVIAWCDHAW
      BEAEUAIUBUBUCVSWEDAVIVSDTAAVIDAADTAHUDADEGFHIVTFLZWODTSAWODUAUEUFZUGUHZVS
      VIVQDWQAVIVIDTSVIWPUCVSVJVPDWQVJVJDTSVSBDFUIUEVSCVODWQJAVOVODTSVIADEEVNGH
      VTVNLZWRDTSAVMDEUJUEIUKUCULUMUMUBAWMVIAWLDHAWFWKAWFOZVLVIWIVQWFVLVIRAVKGF
      UNUOWSWHVPVJWSBCWGVOAWFBCRKUPWFWGVORAVKGVNUQUOURUSUTVAUBUCWNVSWJDVLVJWHBD
      FVBVAVCUEAVIVDWJVRDEGFVEVFVAVGVH $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    reiota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    reiota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    reiota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  The second
       hypothesis is a weakened bound variable condition that allows ~ hbsbc1g
       to be used.  Compare ~ reuuni2f . $)
    reiota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( wcel wreu cv wa cio wceq wb wi ax-17 hbel wal hbreu1 a1i hbiota1 hbeq
      hbbid hbimd hbim1 eleq1 eqeq2 bibi12d imbi2d imbi12d reiota1 ex vtoclgf
      pm2.43i imp ) FEJZACEKZBCLZEJZAMZCNZFOZPZURUSVEQZVAUSAVCUTOZPZQZQURVFQCDF
      EGURVFCCDDFEGDLEJCRSZURUSVECVJUSUSCTQURACEUAUBURBVDCVJHVDVDCTQURCDDVCFVBC
      DUCGUDUBUEUFUGUTFOZVAURVIVFUTFEUHVKVHVEUSVKABVGVDIUTFVCUIUJUKULVAUSVHACEU
      MUNUOUPUQ $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reiota2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  Compare
       ~ reuuni2 . $)
    reiota2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( vy cv wcel ax-17 wal wi a1i reiota2f ) ABCGDEGHEICJBBCKLEDIBCJMFN $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class.  Compare
       ~ reuuniss2 . $)
    reiotass2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) )
         -> ( iota x ( x e. A /\ ph ) ) = ( iota x ( x e. B /\ ps ) ) ) $=
      ( vy wss wi wral wa wrex wreu cv wcel cio wsbc wceq reuss2 simplr
      reiota4 reiotacl ra4sbc sbcimg sylibd syl mpid sylc wb ssel ad2antrr mpd
      simprr hbiota1 hbsbc1g sbceq1a reiota2f syl11anc mpbid eqcomd ) DEGZABHZC
      DIZJZACDKZBCELZJZJZCMZENBJCOZVHDNAJZCOZVGBCVKPZVIVKQZVGACDLZVBVLABCDERZUT
      VBVFSVNVBACVKPZVLACDTVNVKDNZVBVPVLHZHACDUAZVQVBVACVKPVRVACVKDUBABCVKDUCUD
      UEUFUGVGVKENZVEVLVMUHVGVQVTVGVNVQVOVSUEUTVQVTHVBVFDEVKUIUJUKVCVDVEULBVLCF
      EVKVJCFUMZBCFVKEWAUNBCVKUOUPUQURUS $.
      $( [21-Aug-2011] $)
  $}

  ${
    $d F x y $.  $d A x y $.
    $( A definition of function value in terms of iota.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    dffv3 $p |- ( F ` A ) = ( iota x x e. ( F " { A } ) ) $=
      ( vy cv csn cima wcel cab wceq cuni cio cfv abid2 eqeq1i abbii unieqi
      df-iota df-fv 3eqtr4ri ) AECBFGZHZAIZDEFZJZDIZKUAUDJZDIZKUBALBCMUFUHUEUGD
      UCUAUDAUANOPQUBADRDBCST $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d y x F $.  $d y x A $.
    fv4.1 $e |- A e. _V $.
    $( Alternate definition of the value of a function.  The value of a
       function expressed using ` iota ` .  (Contributed by Andrew Salmon,
       1-Aug-2011.) $)
    fv4 $p |- ( F ` A ) = ( iota x A F x ) $=
      ( vy cfv cv wbr wceq wb wal cab cuni cio fv2 dfiota2 eqtr4i ) BCFBAGZCHZR
      EGIJAKELMSANEABCDOSAEPQ $.
      $( [1-Aug-2011] $)
  $}


