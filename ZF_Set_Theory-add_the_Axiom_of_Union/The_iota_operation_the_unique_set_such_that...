$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The iota operation ("the unique set such that...")
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c iota $.
  $( Extend class notation with Russell's definition description binder. $)
  ciota $a class ( iota x ph ) $.

  ${
    $d w x z $.  $d ph w z $.  $d ph w y $.  $d x y $.
    $( Soundness justification theorem for ~ df-iota .  (Contributed by Andrew
       Salmon, 29-Jun-2011.) $)
    iotajust $p |- U. { y | { x | ph } = { y } } =
       U. { z | { x | ph } = { z } } $=
      ( vw cab cv csn wceq sneq eqeq2d cbvabv eqtri unieqi ) ABFZCGZHZIZCFZODGZ
      HZIZDFZSOEGZHZIZEFUCRUFCEPUDIQUEOPUDJKLUFUBEDUDTIUEUAOUDTJKLMN $.
      $( [12-Jul-2011] $) $( [29-Jun-2011] $)
  $}

  ${
    $d y x $.  $d y ph $.
    $( Define Russell's definition description binder, which can be read as
       "the unique ` x ` such that ` ph ` ," where ` ph ` ordinarily contains
       ` x ` as a free variable.  Our definition is meaningful only when there
       is exactly one ` x ` such that ` ph ` is true (see ~ iotaval );
       otherwise, it evaluates to the empty set (see ~ iotanul ). $)
    df-iota $a |- ( iota x ph ) = U. { y | { x | ph } = { y } } $.
  $}

  ${
    $d y x $.  $d y ph $.
    $( Alternate definition for descriptions.  Definition 8.18 in [Quine]
       p. 56.  (Contributed by Andrew Salmon, 30-Jun-2011.) $)
    dfiota2 $p |- ( iota x ph ) = U. { y | A. x ( ph <-> x = y ) } $=
      ( ciota cab cv csn wceq cuni wb wal df-iota df-sn eqeq2i eq2ab bitri
      abbii unieqi eqtri ) ABDABEZCFZGZHZCEZIABFUAHZJBKZCEZIABCLUDUGUCUFCUCTUEB
      EZHUFUBUHTBUAMNAUEBOPQRS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d x y z $.  $d ph y z $.
    $( Bound-variable hypothesis builder for the ` iota ` class.  (Contributed
       by Andrew Salmon, 11-Jul-2011.) $)
    hbiota1 $p |- ( y e. ( iota x ph ) -> A. x y e. ( iota x ph ) ) $=
      ( vz cv wceq wb wal cab cuni wcel ciota hba1 hbab hbuni dfiota2 eleq2i
      albii 3imtr4i ) CEZABEDEFGZBHZDIZJZKZUEBHTABLZKZUGBHBCUCUBBDCUABMNOUFUDTA
      BDPQZUGUEBUHRS $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d w z ph $.  $d w x z $.  $d w y z $.
    hbiota.1 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for the ` iota ` class. $)
    hbiota $p |- ( z e. ( iota y ph ) -> A. x z e. ( iota y ph ) ) $=
      ( vw cv cab csn wceq cuni wcel wal ciota hbab ax-17 hbeq hbuni df-iota
      eleq2i albii 3imtr4i ) DGZACHZFGIZJZFHZKZLZUIBMUCACNZLZUKBMBDUGUFBFDBDDUD
      UEABCDEOUCUELBPQORUJUHUCACFSTZUKUIBULUAUB $.
      $( [23-Aug-2011] $) $( [23-Aug-2011] $)
  $}

  ${
    $d y z $.  $d x z $.  $d ph z $.
    $( Equality theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotaeq $p |- ( A. x x = y -> ( iota x ph ) = ( iota y ph ) ) $=
      ( vz cv wceq wal cab csn cuni ciota wsbc wcel drsb1 df-clab 3bitr4g
      eqrdv eqeq1d abbidv unieqd df-iota 3eqtr4g ) BECEFBGZABHZDEZIZFZDHZJACHZU
      FFZDHZJABKACKUCUHUKUCUGUJDUCUDUIUFUCDUDUIUCABUELACUELUEUDMUEUIMABCDNADBOA
      DCOPQRSTABDUAACDUAUB $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    $( Equivalence theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotabi $p |- ( A. x ( ph <-> ps ) -> ( iota x ph ) = ( iota x ps ) ) $=
      ( vz wb wal cab cv csn wceq cuni ciota eq2ab biimpri eqeq1d abbidv
      unieqd df-iota 3eqtr4g ) ABECFZACGZDHIZJZDGZKBCGZUBJZDGZKACLBCLTUDUGTUCUF
      DTUAUEUBUAUEJTABCMNOPQACDRBCDRS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)

    $( Part of Theorem 8.17 in [Quine] p. 56.  This theorem serves as a lemma
       for the fundamental property of iota.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    uniabio $p |- ( A. x ( ph <-> x = y ) -> U. { x | ph } = y ) $=
      ( cv wceq wb wal cab cuni csn eq2ab biimpri df-sn syl6eqr unieqd visset
      unisn syl6eq ) ABDCDZEZFBGZABHZISJZISUAUBUCUAUBTBHZUCUBUDEUAATBKLBSMNOSCP
      QR $.
      $( [12-Jul-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.19 in [Quine] p. 57.  This theorem is the fundamental property
       of iota.  (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    iotaval $p |- ( A. x ( ph <-> x = y ) -> ( iota x ph ) = y ) $=
      ( vz cv wceq wb wal cab cuni ciota cvv wcel wa wi visset sbeqalb equcomi
      syl6 ax-mp ex equequ2 eqcoms bibi2d biimpd 19.20dv com12 impbid 19.21aiv
      uniabio syl dfiota2 syl5eq ) ABEZCEZFZGZBHZAUNDEZFZGZBHZDIJZUOABKURVBUSUO
      FZGZDHVCUOFURVEDURVBVDURVBVDUOLMZURVBNZVDOCPVFVGUOUSFVDABUOUSLQCDRSTUAVDU
      RVBVDUQVABVDUQVAVDUPUTAUPUTGUOUSCDBUBUCUDUEUFUGUHUIVBDCUJUKABDULUM $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Equivalence between two different forms of ` iota ` .  (Contributed by
       Andrew Salmon, 12-Jul-2011.) $)
    iotaequ $p |- ( E! x ph -> ( iota x ph ) = U. { x | ph } ) $=
      ( vz weu cv wceq wb wal wex ciota cab cuni df-eu iotaval uniabio eqtr4d
      19.23aiv sylbi ) ABDABECEZFGBHZCIABJZABKLZFZABCMTUCCTUASUBABCNABCOPQR $.
      $( [20-Aug-2011] $) $( [12-Jul-2011] $)

    $( Theorem 8.22 in [Quine] p. 57.  This theorem is the result if there
       isn't exactly one ` x ` that satisfies ` ph ` .  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotanul $p |- ( -. E! x ph -> ( iota x ph ) = (/) ) $=
      ( vz weu wn cv wceq wb wal wex ciota c0 df-eu notbii cab cuni alnex ax-1
      eqidd impbid1 con2bid 19.20i eq2ab sylibr dfnul2 syl6eqr sylbir unieqd
      uni0 syl6eq dfiota2 syl5eq sylbi ) ABDZEABFCFZGHBIZCJZEZABKZLGUNUQABCMNUR
      UPCOZPZLUSURVALPLURUTLURUPEZCIZUTLGUPCQVCUTUOUOGZEZCOZLVCUPVEHZCIUTVFGVBV
      GCVBVDUPVBVDVBVBVDRVBUOSTUAUBUPVECUCUDCUEUFUGUHUIUJABCUKULUM $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.23 in [Quine] p. 58.  This theorem proves the existence of the
       ` iota ` class under our definition.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    iotaex $p |- ( iota x ph ) e. _V $=
      ( vz weu ciota cvv wcel cv wceq wb wal wex iotaval eqcomd 19.22i df-eu
      isset 3imtr4i wn c0 iotanul 0ex syl6eqel pm2.61i ) ABDZABEZFGZABHCHZIJBKZ
      CLUHUFIZCLUEUGUIUJCUIUFUHABCMNOABCPCUFQRUESUFTFABUAUBUCUD $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)

    $( Theorem *14.22 in [WhiteheadRussell] p. 190.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    iota3 $p |- ( E! x ph -> [ ( iota x ph ) / x ] ph ) $=
      ( vz weu cv wceq wb wal wex ciota wsbc df-eu wi bi2 19.20i sb2 syl
      iotaval eqcomd dfsbcq mpbid 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABABJZKZAB
      CLUGUICUGABUDKZUIUGUEAMZBHUJUFUKBAUENOABCPQUGUDUHFUJUIGUGUHUDABCRSABUDUHT
      QUAUBUC $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d x ph $.
    iotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for iota. $)
    iotabidv $p |- ( ph -> ( iota x ps ) = ( iota x ch ) ) $=
      ( wb wal ciota wceq 19.21aiv iotabi syl ) ABCFZDGBDHCDHIAMDEJBCDKL $.
      $( [20-Aug-2011] $) $( [20-Aug-2011] $)
  $}

  $( Membership law for descriptions.  (Contributed by Andrew Salmon,
     1-Aug-2011.) $)
  iotacl $p |- ( E! x ph -> ( iota x ph ) e. { x | ph } ) $=
    ( weu ciota wsbc cab wcel iota3 iotaex elabs sylibr ) ABCABABDZELABFGABHABL
    ABIJK $.
    $( [1-Aug-2011] $) $( [1-Aug-2011] $)

  $( Property of iota.  Compare ~ euuni . $)
  iota1 $p |- ( E! x ph -> ( ph <-> ( iota x ph ) = x ) ) $=
    ( weu cab cuni cv wceq ciota euuni iotaequ eqeq1d bitr4d ) ABCZAABDEZBFZGAB
    HZOGABIMPNOABJKL $.
    $( [23-Aug-2011] $) $( [23-Aug-2011] $)

  $( Membership law for descriptions.  Compare ~ reucl2 . $)
  riotacl2 $p |- ( E! x e. A ph
                  -> ( iota x ( x e. A /\ ph ) ) e. { x e. A | ph } ) $=
    ( wreu cv wcel wa ciota cab crab weu df-reu iotacl sylbi df-rab syl6eleqr
    ) ABCDZBECFAGZBHZRBIZABCJQRBKSTFABCLRBMNABCOP $.
    $( [21-Aug-2011] $) $( [21-Aug-2011] $)

  ${
    $d x A $.
    $( Membership law for descriptions.  Compare ~ reucl . $)
    riotacl $p |- ( E! x e. A ph
                    -> ( iota x ( x e. A /\ ph ) ) e. A ) $=
      ( wreu crab cv wcel wa ciota wss ssrab2 a1i riotacl2 sseldd ) ABCDZABCEZC
      BFCGAHBIPCJOABCKLABCMN $.
      $( [21-Aug-2011] $) $( [21-Aug-2011] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  riota4 $p |- ( E! x e. A ph -> [ ( iota x ( x e. A /\ ph ) ) / x ] ph ) $=
    ( wreu cv wcel wa ciota wsbc weu df-reu iota3 sylbi wi cvv iotaex pm3.27
    sbcth ax-mp wb sbcimg mpbi syl ) ABCDZBECFZAGZBUFBHZIZABUGIZUDUFBJUHABCKUFB
    LMUFANZBUGIZUHUINZUGOFZUKUFBPZUJBUGOUEAQRSUMUKULTUNUFABUGOUASUBUC $.
    $( [23-Aug-2011] $) $( [23-Aug-2011] $)

  $( Property of iota.  Compare ~ reuuni1 . $)
  riota1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
          ( ph <-> ( iota x ( x e. A /\ ph ) ) = x ) ) $=
    ( cv wcel wa wreu ciota wceq ibar weu wb df-reu iota1 sylbi sylan9bb ) BDZC
    EZARAFZABCGZSBHQIZRAJTSBKSUALABCMSBNOP $.
    $( [23-Aug-2011] $) $( [23-Aug-2011] $)


  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    riota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  The second
       hypothesis is a weakened bound variable condition that allows ~ hbsbc1g
       to be used.  Compare ~ reuuni2f . $)
    riota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( wcel wreu cv wa ciota wceq wb wi ax-17 hbel wal hbreu1 a1i hbiota1
      hbeq hbbid hbimd hbim1 eleq1 eqeq2 bibi12d imbi2d imbi12d riota1 ex
      vtoclgf pm2.43i imp ) FEJZACEKZBCLZEJZAMZCNZFOZPZURUSVEQZVAUSAVCUTOZPZQZQ
      URVFQCDFEGURVFCCDDFEGDLEJCRSZURUSVECVJUSUSCTQURACEUAUBURBVDCVJHVDVDCTQURC
      DDVCFVBCDUCGUDUBUEUFUGUTFOZVAURVIVFUTFEUHVKVHVEUSVKABVGVDIUTFVCUIUJUKULVA
      USVHACEUMUNUOUPUQ $.
      $( [23-Aug-2011] $) $( [23-Aug-2011] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    riota2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  Compare
       ~ reuuni2 . $)
    riota2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( vy cv wcel ax-17 wal wi a1i riota2f ) ABCGDEGHEICJBBCKLEDIBCJMFN $.
      $( [23-Aug-2011] $) $( [23-Aug-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class.  Compare
       ~ reuuniss2 . $)
    riotass2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) )
         -> ( iota x ( x e. A /\ ph ) ) = ( iota x ( x e. B /\ ps ) ) ) $=
      ( vy wss wi wral wa wrex wreu cv wcel ciota wsbc wceq riota4 riotacl
      ra4sbc sbcimg sylibd syl mpid reuss2 simplr sylc wb hbiota1 hbsbc1g
      sbceq1a riota2f ssel ad2antrr mpd simprr sylanc mpbid eqcomd ) DEGZABHZCD
      IZJZACDKZBCELZJZJZCMZENBJCOZVHDNAJZCOZVGBCVKPZVIVKQZACDLZVBVLVGVNVBACVKPZ
      VLACDRVNVKDNZVBVOVLHZHACDSZVPVBVACVKPVQVACVKDTABCVKDUAUBUCUDABCDEUEZUTVBV
      FUFUGVKENZVEVLVMUHVGBVLCFEVKVJCFUIZBCFVKEWAUJBCVKUKULVGVPVTVGVNVPVSVRUCUT
      VPVTHVBVFDEVKUMUNUOVCVDVEUPUQURUS $.
      $( [21-Aug-2011] $) $( [21-Aug-2011] $)
  $}


