$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equinumerosity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Introduce new constant symbols. $)
  $c ~~ $.  $( Equinumerosity relation ("approximately equals" symbol) $)
  $c ~<_ $.  $( Dominance relation (curly less-than-or-equal) $)
  $c ~< $.  $( Strict dominance relation (curly less-than) $)
  $c Fin $.  $( Class of all finite sets. $)

  $( Extend class definition to include the equinumerosity relation
     ("approximately equals" symbol) $)
  cen $a class ~~ $.

  $( Extend class definition to include the dominance relation (curly
     less-than-or-equal) $)
  cdom $a class ~<_ $.

  $( Extend class definition to include the strict dominance relation (curly
     less-than) $)
  csdm $a class ~< $.

  $( Extend class definition to include the class of all finite sets. $)
  cfn $a class Fin $.

  ${
    $d x y f $.
    $( Define the equinumerosity relation.  Definition of [Enderton] p. 129.
       We define ` ~~ ` to be a binary relation rather than a connective, so
       its arguments must be sets to be meaningful.  This is acceptable because
       we do not consider equinumerosity for proper classes.  We derive the
       usual definition as ~ bren . $)
    df-en $a |- ~~ = { <. x , y >. | E. f f : x -1-1-onto-> y } $.

    $( Define the dominance relation.  For an alternate definition see
       ~ dfdom2 .  Compare Definition of [Enderton] p. 145.  Typical textbook
       definitions are derived as ~ brdom and ~ domen . $)
    df-dom $a |- ~<_ = { <. x , y >. | E. f f : x -1-1-> y } $.

    $( Define the strict dominance relation.  Alternate possible definitions
       are derived as ~ brsdom and ~ brsdom2 .  Definition 3 of [Suppes]
       p. 97. $)
    df-sdom $a |- ~< = ( ~<_ \ ~~ ) $.

    $( Define the (proper) class of all finite sets.  Similar to Definition
       10.29 of [TakeutiZaring] p. 91, whose "Fin(a)" corresponds to
       our " ` a e. Fin ` ".  This definition is meaningful whether or not we
       accept the Axiom of Infinity ~ ax-inf2 .  If we accept Infinity, we can
       also express ` A e. Fin ` by ` A ~< om ` (theorem ~ isfinite .) $)
    df-fin $a |- Fin = { x | E. y e. om x ~~ y } $.
  $}

  ${
    $d x y f $.  $d x y f $.
    $( Equinumerosity is a relation. $)
    relen $p |- Rel ~~ $=
      ( vx vy vf cen wrel cv wf1o wex copab relopab df-en releqi mpbir ) DEAFBF
      CFGCHZABIZENABJDOABCKLM $.
      $( [28-Mar-1998] $)

    $( Dominance is a relation. $)
    reldom $p |- Rel ~<_ $=
      ( vx vy vf cdom wrel cv wf1 wex copab relopab df-dom releqi mpbir ) DEAFB
      FCFGCHZABIZENABJDOABCKLM $.
      $( [28-Mar-1998] $)
  $}

  $( Strict dominance is a relation. $)
  relsdom $p |- Rel ~< $=
    ( cdom wrel csdm reldom cen cdif reldif df-sdom releqi sylibr ax-mp ) ABZCB
    ZDLAEFZBMAEGCNHIJK $.
    $( [31-Mar-1998] $)

  ${
    $d f x y A $.  $d f x y B $.  $d y C $.
    $( Equinumerosity relation. $)
    breng $p |- ( B e. C -> ( A ~~ B <-> E. f f : A -1-1-onto-> B ) ) $=
      ( vx vy cvv wcel cen wbr cv wf1o wex wb wi wceq f1oeq2 exbidv f1oeq3
      df-en brabg ex wn relen brrelexi wfn f1ofn cdm fndm visset dmex
      syl6eqelr syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZNZOU
      RUSVDEKZFKZVALZDMAVFVALZDMVCEFABGCIVEAPVGVHDVEAVFVAQRVFBPVHVBDVFBAVASREFD
      TUAUBURUCVDUSUTURVCABIUDUEVBURDVBVAAUFZURABVAUGVIAVAUHGAVAUIVADUJUKULUMUN
      UOUPUQ $.
      $( [15-Jun-1998] $)

    $( Dominance relation. $)
    brdomg $p |- ( B e. C -> ( A ~<_ B <-> E. f f : A -1-1-> B ) ) $=
      ( vx vy cvv wcel cdom wbr cv wf1 wex wb wi wceq f1eq2 exbidv f1eq3
      df-dom brabg ex wn reldom brrelexi wf f1f cdm fdm visset dmex syl6eqelr
      syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZNZOURUSVDEKZFK
      ZVALZDMAVFVALZDMVCEFABGCIVEAPVGVHDVEAVFVAQRVFBPVHVBDVFBAVASREFDTUAUBURUCV
      DUSUTURVCABIUDUEVBURDVBABVAUFZURABVAUGVIAVAUHGABVAUIVADUJUKULUMUNUOUPUQ
      $.
      $( [15-Jun-1998] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.  $d f w x y z $.
    bren.1 $e |- B e. _V $.
    $( Equinumerosity relation.  Compare Definition of [Enderton] p. 129. $)
    bren $p |- ( A ~~ B <-> E. f f : A -1-1-onto-> B ) $=
      ( cvv wcel cen wbr cv wf1o wex wb breng ax-mp ) BEFABGHABCIJCKLDABECMN $.
      $( [15-Jun-1998] $)

    $( Dominance relation. $)
    brdom $p |- ( A ~<_ B <-> E. f f : A -1-1-> B ) $=
      ( cvv wcel cdom wbr cv wf1 wex wb brdomg ax-mp ) BEFABGHABCIJCKLDABECMN
      $.
      $( [15-Jun-1998] $)

    $( Dominance in terms of equinumerosity.  Example 1 of [Enderton]
       p. 146. $)
    domen $p |- ( A ~<_ B <-> E. x ( A ~~ x /\ x C_ B ) ) $=
      ( vf cv wf1 wex wf1o wss wa cdom wbr cen visset f11o exbii excom bitri
      brdom bren anbi1i 19.41v bitr4i 3bitr4i ) BCEFZGZEHZBAFZUFIZUICJZKZEHZAHZ
      BCLMBUINMZUKKZAHUHULAHZEHUNUGUQEABCUFEOPQULEARSBCEDTUPUMAUPUJEHZUKKUMUOUR
      UKBUIEAOUAUBUJUKEUCUDQUE $.
      $( [15-Jun-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance in terms of equinumerosity, with the sethood requirement
       expressed as an antecedent.  Example 1 of [Enderton] p. 146. $)
    domeng $p |- ( B e. C -> ( A ~<_ B <-> E. x ( A ~~ x /\ x C_ B ) ) ) $=
      ( vy cv cdom wbr cen wss wa wex breq2 wceq sseq2 anbi2d exbidv visset
      domen vtoclbg ) BEFZGHBAFZIHZUBUAJZKZALBCGHUCUBCJZKZALECDUACBGMUACNZUEUGA
      UHUDUFUCUACUBOPQABUAERST $.
      $( [24-Apr-2004] $)
  $}

  $( Strict dominance relation, meaning " ` B ` is strictly greater in size
     than ` A ` ."  Definition of [Mendelson] p. 255. $)
  brsdom $p |- ( A ~< B <-> ( A ~<_ B /\ -. A ~~ B ) ) $=
    ( cop csdm wcel cdom cen cdif wbr wn wa df-sdom eleq2i df-br notbii
    anbi12i eldif bitr4i 3bitr4i ) ABCZDETFGHZEZABDIABFIZABGIZJZKZDUATLMABDNUFT
    FEZTGEZJZKUBUCUGUEUIABFNUDUHABGNOPTFGQRS $.
    $( [25-Jun-1998] $)

  ${
    $d x y f A $.
    $( Express " ` A ` is finite."  Definition 10.29 of [TakeutiZaring] p. 91
       (whose " ` Fin ` " is a predicate instead of a class). $)
    isfi $p |- ( A e. Fin <-> E. x e. om A ~~ x ) $=
      ( vy cfn wcel cv cen wbr com wrex cab df-fin eleq2i cvv relen brrelexi
      r19.23aivw wceq breq1 rexbidv elab3 bitri ) BDEBCFZAFZGHZAIJZCKZEBUDGHZAI
      JZDUGBCALMUFUICBUHBNEAIBUDGOPQUCBRUEUHAIUCBUDGSTUAUB $.
      $( [22-Aug-2008] $)

    $( Equinumerosity implies dominance. $)
    enssdom $p |- ~~ C_ ~<_ $=
      ( vx vy vf cen cdom relen cv cop wf1o wex copab wcel wf1 f1of1 eximi
      opabid 3imtr4i df-en eleq2i df-dom relssi ) ABDEFAGZBGZHZUBUCCGZIZCJZABKZ
      LZUDUBUCUEMZCJZABKZLZUDDLUDELUGUKUIUMUFUJCUBUCUENOUGABPUKABPQDUHUDABCRSEU
      LUDABCTSQUA $.
      $( [31-Mar-1998] $)
  $}

  $( Alternate definition of dominance. $)
  dfdom2 $p |- ~<_ = ( ~< u. ~~ ) $=
    ( cen csdm cun cdom cdif df-sdom uneq2i uncom wss wceq enssdom undif mpbi
    3eqtr3ri ) ABCADAEZCZBACDBOAFGABHADIPDJKADLMN $.
    $( [17-Jun-1998] $)

  $( Equinumerosity implies dominance.  Theorem 15 of [Suppes] p. 94. $)
  endom $p |- ( A ~~ B -> A ~<_ B ) $=
    ( cen cdom enssdom ssbri ) CDABEF $.
    $( [28-May-1998] $)

  $( Strict dominance implies dominance. $)
  sdomdom $p |- ( A ~< B -> A ~<_ B ) $=
    ( csdm wbr cdom cen wn brsdom simplbi ) ABCDABEDABFDGABHI $.
    $( [10-Jun-1998] $)

  $( Strict dominance implies non-equinumerosity. $)
  sdomnen $p |- ( A ~< B -> -. A ~~ B ) $=
    ( csdm wbr cdom cen wn brsdom simprbi ) ABCDABEDABFDGABHI $.
    $( [10-Jun-1998] $)

  $( Dominance in terms of strict dominance and equinumerosity.  Theorem 22(iv)
     of [Suppes] p. 97. $)
  brdom2 $p |- ( A ~<_ B <-> ( A ~< B \/ A ~~ B ) ) $=
    ( cop cdom wcel csdm cen cun wbr wo dfdom2 eleq2i df-br orbi12i elun
    bitr4i 3bitr4i ) ABCZDERFGHZEZABDIABFIZABGIZJZDSRKLABDMUCRFEZRGEZJTUAUDUBUE
    ABFMABGMNRFGOPQ $.
    $( [17-Jun-1998] $)

  $( Equinumerosity expressed in terms of dominance and strict dominance. $)
  bren2 $p |- ( A ~~ B <-> ( A ~<_ B /\ -. A ~< B ) ) $=
    ( cen wbr cdom csdm wn wa endom sdomnen con2i jca wo brdom2 biimpi orcanai
    impbii ) ABCDZABEDZABFDZGZHRSUAABITRABJKLSTRSTRMABNOPQ $.
    $( [23-Oct-2004] $)

  ${
    $d f A $.
    $( Equinumerosity is reflexive.  Theorem 1 of [Suppes] p. 92. $)
    enrefg $p |- ( A e. B -> A ~~ A ) $=
      ( vf wcel cen wbr cv wf1o wex cid cres cvv resiexg f1oi f1oeq1 cla4egv
      mpi syl breng mpbird ) ABDZAAEFAACGZHZCIZUAJAKZLDZUDABMUFAAUEHZUDANUCUGCU
      ELAAUBUEOPQRAABCST $.
      $( [20-Jan-2007] $) $( [18-Jun-1998] $)
  $}

  ${
    enref.1 $e |- A e. _V $.
    $( Equinumerosity is reflexive.  Theorem 1 of [Suppes] p. 92. $)
    enref $p |- A ~~ A $=
      ( cvv wcel cen wbr enrefg ax-mp ) ACDAAEFBACGH $.
      $( [25-Sep-2004] $)
  $}

  $( Equality implies equinumerosity. $)
  eqeng $p |- ( A e. C -> ( A = B -> A ~~ B ) ) $=
    ( wceq cen wbr wcel breq2 enrefg syl5cbi ) ABDAAEFABEFACGABAEHACIJ $.
    $( [26-Oct-2003] $)

  $( Dominance is reflexive. $)
  domrefg $p |- ( A e. B -> A ~<_ A ) $=
    ( wcel cen wbr cdom enrefg endom syl ) ABCAADEAAFEABGAAHI $.
    $( [18-Jun-1998] $)

  ${
    $d f A $.  $d f B $.  $d f F $.
    $( The domain and range of a one-to-one, onto function are equinumerous.
       This variation of ~ f1oeng does not require the Axiom of Replacement. $)
    f1oen2g $p |- ( ( F e. C /\ F : A -1-1-onto-> B ) -> A ~~ B ) $=
      ( vf wcel wf1o wa cvv cv wex cen wbr wf dmfex f1of sylan2 f1oeq1 cla4egv
      imp wi wn brprc enrefg syl5bir a1d com3r breng biimprd pm2.61d2 sylc ) DC
      FZABDGZHAIFZABEJZGZEKZABLMZULABDNUNUMABCDOABDPQULUMUQUPUMEDCABUODRSTUNBIF
      ZUQURUAUSUBZUQUNURUTUNURUAUQUTURAALMUNABLUCAIUDUEUFUGUSURUQABIEUHUIUJUK
      $.
      $( [13-Jan-2007] $)

    $( The domain and range of a one-to-one, onto function are equinumerous. $)
    f1oeng $p |- ( ( A e. C /\ F : A -1-1-onto-> B ) -> A ~~ B ) $=
      ( wcel wf1o cvv cen wbr wfn fnex ancoms f1ofn sylan2 f1oen2g sylancom )
      ACEZABDFZDGEZABHIQDAJZSRTQSACDKLABDMNABGDOP $.
      $( [30-Jan-2007] $) $( [19-Jun-1998] $)

    $( The domain of a one-to-one function is dominated by its codomain. $)
    f1domg $p |- ( A e. C -> ( F : A -1-1-> B -> A ~<_ B ) ) $=
      ( vf wcel wf1 cv wex cdom wbr cvv wf fex f1f sylan expcom f1eq1 cla4egv
      syli wi wn brprc domrefg syl5bir a1d com3r brdomg biimprd pm2.61d2 syld
      ) ACFZABDGZABEHZGZEIZABJKZUMULDLFZUPUMULURABDMULURUMABCDNABDOPQUOUMEDLABU
      NDRSTULBLFZUPUQUAUSUBZUPULUQUTULUQUAUPUTUQAAJKULABJUCACUDUEUFUGUSUQUPABLE
      UHUIUJUK $.
      $( [18-Jun-1998] $)

    $( The domain of a one-to-one function is dominated by its codomain. $)
    f1dom2g $p |- ( B e. C -> ( F : A -1-1-> B -> A ~<_ B ) ) $=
      ( wf1 wcel cvv cdom wbr f1dmex expcom f1domg syli ) ABDEZBCFZAGFZABHINOPA
      BCDJKABGDLM $.
      $( [4-Sep-2004] $)
  $}

  ${
    f1oen.1 $e |- A e. _V $.
    $( The domain and range of a one-to-one, onto function are equinumerous. $)
    f1oen $p |- ( F : A -1-1-onto-> B -> A ~~ B ) $=
      ( cvv wcel wf1o cen wbr f1oeng mpan ) AEFABCGABHIDABECJK $.
      $( [19-Jun-1998] $)

    $( The domain of a one-to-one function is dominated by its codomain. $)
    f1dom $p |- ( F : A -1-1-> B -> A ~<_ B ) $=
      ( cvv wcel wf1 cdom wbr wi f1domg ax-mp ) AEFABCGABHIJDABECKL $.
      $( [19-Jun-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.  $d x y ph $.
    en2d.1 $e |- ( ph -> A e. _V ) $.
    en2d.2 $e |- ( ph -> ( x e. A -> C e. _V ) ) $.
    en2d.3 $e |- ( ph -> ( y e. B -> D e. _V ) ) $.
    en2d.4 $e |- ( ph -> ( ( x e. A /\ y = C ) <-> ( y e. B /\ x = D ) ) ) $.
    $( Equinumerosity inference from an implicit one-to-one onto function. $)
    en2d $p |- ( ph -> A ~~ B ) $=
      ( cvv wcel cv wceq wa copab wf1o cen wbr wfn ccnv dff1o4 weu wral eueq
      syl6ib r19.21aiv eqid fnopabg sylib opabbidv fneq1d mpbid cnvopab
      sylanbrc f1oeng syl11anc ) ADLMDECNZEMZBNZGOZPZBCQZRZDESTHVEVDDUAZVDUBZEU
      AZADEVDUCAVADMZUSFOZPZBCQZDUAZVFAVJCUDZBDUEVMAVNBDAVIFLMVNICFUFUGUHVJBCDV
      LVLUIUJUKADVLVDAVKVCBCKULUMUNAVBBUDZCEUEVHAVOCEAUTGLMVOJBGUFUGUHVBCBEVGVC
      BCUOUJUKUPDELVDUQUR $.
      $( [27-Jul-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.  $d x y ph $.
    en3d.1 $e |- ( ph -> A e. _V ) $.
    en3d.2 $e |- ( ph -> ( x e. A -> C e. B ) ) $.
    en3d.3 $e |- ( ph -> ( y e. B -> D e. A ) ) $.
    en3d.4 $e |- ( ph -> ( ( x e. A /\ y e. B ) -> ( x = D <-> y = C ) ) ) $.
    $( Equinumerosity inference from an implicit one-to-one onto function. $)
    en3d $p |- ( ph -> A ~~ B ) $=
      ( cv wcel cvv elisset syl6 wceq wa wi eleq1a imp32 wb imp biimpar exp42
      com34 jcai biimpa com23 impbida en2d ) ABCDEFGHABLZDMZFEMZFNMIFEOPACLZEMZ
      GDMZGNMJGDOPAUMUOFQZRZUPULGQZRZAUSRUPUTAUMURUPAUMUNURUPSIFEUOTPUAAUMURUPU
      TSAUMUPURUTAUMUPURUTAUMUPRZRZUTURAVBUTURUBKUCZUDUEUFUAUGAVARUMURAUPUTUMAU
      PUQUTUMSJGDULTPUAAUPUTUMURSAUPUMUTURAUMUPUTURSAUMUPUTURVCUTURVDUHUEUIUFUA
      UGUJUK $.
      $( [27-Jul-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    en2.1 $e |- A e. _V $.
    en2.2 $e |- ( x e. A -> C e. _V ) $.
    en2.3 $e |- ( y e. B -> D e. _V ) $.
    en2.4 $e |- ( ( x e. A /\ y = C ) <-> ( y e. B /\ x = D ) ) $.
    $( Equinumerosity inference from an implicit one-to-one onto function. $)
    en2 $p |- A ~~ B $=
      ( wceq cen wbr eqid cvv wcel a1i cv wi wa wb en2d ax-mp ) CCKZCDLMCNUDABC
      DEFCOPUDGQARZCPZEOPSUDHQBRZDPZFOPSUDIQUFUGEKTUHUEFKTUAUDJQUBUC $.
      $( [4-Jan-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    en3.1 $e |- A e. _V $.
    en3.2 $e |- ( x e. A -> C e. B ) $.
    en3.3 $e |- ( y e. B -> D e. A ) $.
    en3.4 $e |- ( ( x e. A /\ y e. B ) -> ( x = D <-> y = C ) ) $.
    $( Equinumerosity inference from an implicit one-to-one onto function. $)
    en3 $p |- A ~~ B $=
      ( wceq cen wbr eqid cvv wcel a1i cv wi wa wb en3d ax-mp ) CCKZCDLMCNUDABC
      DEFCOPUDGQARZCPZEDPSUDHQBRZDPZFCPSUDIQUFUHTUEFKUGEKUASUDJQUBUC $.
      $( [19-Jul-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y z C $.  $d x z D $.  $d x y ph $.
    dom2d.1 $e |- ( ph -> ( x e. A -> C e. B ) ) $.
    dom2d.2 $e |- ( ph -> ( ( x e. A /\ y e. A ) -> ( C = D <-> x = y ) ) ) $.
    $( A mapping (first hypothesis) that is one-to-one (second hypothesis)
       implies its domain is dominated by its codomain. $)
    dom2d $p |- ( ph -> ( A e. R -> A ~<_ B ) ) $=
      ( vz wcel cv wceq wa copab wf1 cdom wbr f1domg wf cfv wi wral hbopab1
      hbopab2 dff13f r19.21aiv eqid fopab2 sylib imp fvopab2 adantll mpdan
      adantrr ax-17 hbfv hbeq hbim eleq1 anbi2d imbi1d wb anbi1d anidm syl6bb
      fveq2 adantr biimparc eqeq12d ex sylbird pm5.74d bitrd chvar adantrl
      biimpd sylbid r19.21aivv sylanbrc syl5com ) DHLDEBMZDLZCMZFNOZBCPZQZDERSA
      DEHWGTWHDEWGUAZWCWGUBZWEWGUBZNZWCWENZUCZCDUDBDUDABCKDEWGWFBCKUEZWFBCKUFUG
      AFELZBDUDWIAWPBDIUHBCDEFWGWGUIUJUKAWNBCDDAWDWEDLZOZWNAWROZWLFGNZWMWSWJFWK
      GAWDWJFNZWQAWDOZWPXAAWDWPIULWDWPXAABCDFEUMUNUOZUPAWQWKGNZWDXBXAUCZAWQOZXD
      UCZBCXFXDBXFBUQBKKWKGBKWEWGWOKMZWELBUQURXHGLBUQUSUTWMXEXFXAUCXGWMXBXFXAWM
      WDWQAWCWEDVAZVBVCWMXFXAXDWMXFWSXAXDVDZWMWRWQAWMWRWQWQOWQWMWDWQWQXIVEWQVFV
      GVBWMWSXJWMWSOWJWKFGWMWLWSWCWEWGVHVIWSWTWMAWRWTWMVDJULZVJVKVLVMVNVOXCVPVQ
      VKWSWTWMXKVRVSVLVTWAWB $.
      $( [24-Jul-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    dom2.1 $e |- ( x e. A -> C e. B ) $.
    dom2.2 $e |- ( ( x e. A /\ y e. A ) -> ( C = D <-> x = y ) ) $.
    $( A mapping (first hypothesis) that is one-to-one (second hypothesis)
       implies its domain is dominated by its codomain. ` C ` and ` D ` can be
       read ` C ( x ) ` and ` D ( y ) ` , as can be shown from their distinct
       variable conditions. $)
    dom2 $p |- ( A e. R -> A ~<_ B ) $=
      ( wceq wcel cdom wbr wi eqid cv a1i wa wb dom2d ax-mp ) CCJZCGKCDLMNCOUBA
      BCDEFGAPZCKZEDKNUBHQUDBPZCKREFJUCUEJSNUBIQTUA $.
      $( [26-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( Equality implies equinumerosity. $)
    idssen $p |- _I C_ ~~ $=
      ( vx vy cid cen reli cv wbr cop wcel wceq visset ideq cres wf1o f1oi
      f1oeq3 mpbii f1oen syl sylbi df-br 3imtr3i relssi ) ABCDEAFZBFZCGZUDUEDGZ
      UDUEHZCIUHDIUFUDUEJZUGUDUEBKLUIUDUECUDMZNZUGUIUDUDUJNUKUDOUDUEUDUJPQUDUEU
      JAKRSTUDUECUAUDUEDUAUBUC $.
      $( [30-Apr-1998] $)
  $}

  $( The domain of equinumerosity. $)
  dmen $p |- dom ~~ = _V $=
    ( cvv cen cdm wss wceq cid idssen dmss dmi syl5ssr ax-mp vss mpbi ) ABCZDZN
    AEFBDZOGPFCNAFBHIJKNLM $.
    $( [10-May-1998] $)

  ${
    $d x A $.  $d x B $.
    $( A set dominates its subsets.  Theorem 16 of [Suppes] p. 94. $)
    ssdomg $p |- ( A e. C -> ( A C_ B -> A ~<_ B ) ) $=
      ( wcel cid cres wf1 cdom wbr wss f1domg wf ccnv wfun wa wfo wf1o f1oi
      dff1o3 mpbi simpli fof ax-mp fss mpan funi wceq wb cnvi funeq mpbir
      funres11 jctir df-f1 sylibr syl5 ) ACDABEAFZGZABHIABJZABCUQKUSABUQLZUQMNZ
      OURUSUTVAAAUQLZUSUTAAUQPZVBVCVAAAUQQVCVAOARAAUQSTUAAAUQUBUCAABUQUDUEEMZNZ
      VAVEENZUFVDEUGVEVFUHUIVDEUJUCUKAEULUCUMABUQUNUOUP $.
      $( [19-Jun-1998] $)
  $}

  $( A set dominates its subsets.  Theorem 16 of [Suppes] p. 94. $)
  ssdom2g $p |- ( B e. C -> ( A C_ B -> A ~<_ B ) ) $=
    ( wss wcel cvv cdom wbr ssexg expcom ssdomg syli ) ABDZBCEZAFEZABGHMNOABCIJ
    ABFKL $.
    $( [19-Jun-1998] $)

  ${
    $d f g x y z $.
    $( Equinumerosity is an equivalence relation. $)
    ener $p |- Er ~~ $=
      ( vx vy vz vf vg cen cv wbr wf1o wex visset bren ccnv f1ocnv f1oen syl
      19.23aiv sylbi wa eeanv ccom f1oco ancoms 19.23aivv sylbir syl2anb ster
      ) ABCFAGZBGZFHZUHUIDGZIZDJUIUHFHZUHUIDBKZLULUMDULUIUHUKMZIUMUHUIUKNUIUHUO
      UNOPQRUHUIEGZIZEJZUICGZUKIZDJZUHUSFHZUJUIUSFHURVASUQUTSZDJEJVBUQUTEDTVCVB
      EDVCUHUSUKUPUAZIZVBUTUQVEUHUIUSUKUPUBUCUHUSVDAKOPUDUEUHUIEUNLUIUSDCKLUFUG
      $.
      $( [19-Mar-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Symmetry of equinumerosity.  Theorem 2 of [Suppes] p. 92. $)
    ensymg $p |- ( B e. C -> ( A ~~ B -> B ~~ A ) ) $=
      ( vx vy wcel cen wbr cvv wi relen brrelexi cv wceq breq1 breq2 imbi12d
      visset ener ersym vtocl2g ex syl pm2.43b ) BCFZABGHZBAGHZUFAIFZUEUFUGJZJA
      BGKLUHUEUIDMZEMZGHZUKUJGHZJAUKGHZUKAGHZJUIDEABICUJANULUNUMUOUJAUKGOUJAUKG
      PQUKBNUNUFUOUGUKBAGPUKBAGOQUJUKGDRERSTUAUBUCUD $.
      $( [26-Oct-2003] $)
  $}

  ${
    ensym.1 $e |- B e. _V $.
    $( Symmetry of equinumerosity.  Theorem 2 of [Suppes] p. 92. $)
    ensym $p |- ( A ~~ B -> B ~~ A ) $=
      ( cvv wcel cen wbr wi ensymg ax-mp ) BDEABFGBAFGHCABDIJ $.
      $( [25-Sep-2004] $)
    ${
      ensymi.2 $e |- A ~~ B $.
      $( Symmetry of equinumerosity.  Theorem 2 of [Suppes] p. 92. $)
      ensymi $p |- B ~~ A $=
        ( cen wbr ensym ax-mp ) ABEFBAEFDABCGH $.
        $( [25-Sep-2004] $)
    $}
  $}

  ${
    $d x y z f g h A $.  $d x y z f g B $.  $d x y z f g h C $.
    $( Transitivity of equinumerosity.  Theorem 3 of [Suppes] p. 92. $)
    entr $p |- ( ( A ~~ B /\ B ~~ C ) -> A ~~ C ) $=
      ( vx vy vz cen relen cv visset ener ertr enref vtoclrbr ) DEFABCGHDIZEIFI
      GDJZEJFJKLOPMN $.
      $( [9-Jun-1998] $)

    $( Transitivity of dominance relation.  Theorem 17 of [Suppes] p. 94. $)
    domtr $p |- ( ( A ~<_ B /\ B ~<_ C ) -> A ~<_ C ) $=
      ( vx vy vz vg vf cdom reldom cv wf1 wex wbr wa eeanv ccom f1co ancoms
      visset f1dom syl 19.23aivv sylbir brdom syl2anb cvv wcel domrefg ax-mp
      vtoclrbr ) DEFABCIJDKZEKZGKZLZGMZUMFKZHKZLZHMZULUQINZULUMINUMUQINUPUTOUOU
      SOZHMGMVAUOUSGHPVBVAGHVBULUQURUNQZLZVAUSUOVDULUMUQURUNRSULUQVCDTZUAUBUCUD
      ULUMGETUEUMUQHFTUEUFULUGUHULULINVEULUGUIUJUK $.
      $( [4-Jun-1998] $)
  $}

  ${
    entri.1 $e |- A ~~ B $.
    entri.2 $e |- B ~~ C $.
    $( A chained equinumerosity inference. $)
    entri $p |- A ~~ C $=
      ( cen wbr entr mp2an ) ABFGBCFGACFGDEABCHI $.
      $( [25-Sep-2004] $)
  $}

  ${
    entr2i.1 $e |- C e. _V $.
    entr2i.2 $e |- A ~~ B $.
    entr2i.3 $e |- B ~~ C $.
    $( A chained equinumerosity inference. $)
    entr2i $p |- C ~~ A $=
      ( entri ensymi ) ACDABCEFGH $.
      $( [25-Sep-2004] $)
  $}

  ${
    entr3i.1 $e |- B e. _V $.
    entr3i.2 $e |- A ~~ B $.
    entr3i.3 $e |- A ~~ C $.
    $( A chained equinumerosity inference. $)
    entr3i $p |- B ~~ C $=
      ( ensymi entri ) BACABDEGFH $.
      $( [25-Sep-2004] $)
  $}

  ${
    entr4i.1 $e |- B e. _V $.
    entr4i.2 $e |- A ~~ B $.
    entr4i.3 $e |- C ~~ B $.
    $( A chained equinumerosity inference. $)
    entr4i $p |- A ~~ C $=
      ( ensymi entri ) ABCECBDFGH $.
      $( [25-Sep-2004] $)
  $}

  $( Transitivity of equinumerosity and dominance. $)
  endomtr $p |- ( ( A ~~ B /\ B ~<_ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan ) ABDEBCDEACDEABFEABCGABHI $.
    $( [7-Jun-1998] $)

  $( Transitivity of dominance and equinumerosity. $)
  domentr $p |- ( ( A ~<_ B /\ B ~~ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan2 ) ABDEBCDEACDEBCFEABCGBCHI $.
    $( [7-Jun-1998] $)

  ${
    f1imaen.1 $e |- C e. _V $.
    $( A one-to-one function's image under a subset of its domain is
       equinumerous to the subset. $)
    f1imaen $p |- ( ( F : A -1-1-> B /\ C C_ A ) -> ( F " C ) ~~ C ) $=
      ( wf1 wss wa cima cres wf1o cen wbr f1ores f1oen wfo cvv wcel wi f1ofo
      fornex ax-mp ensymg 3syl mpd syl ) ABDFCAGHCDCIZDCJZKZUGCLMZABCDNUICUGLMZ
      UJCUGUHEOUICUGUHPZUGQRZUKUJSCUGUHTCQRULUMSECUGQUHUAUBCUGQUCUDUEUF $.
      $( [30-Sep-2004] $)
  $}

  ${
    $d f A $.
    $( The empty set is equinumerous only to itself.  Exercise 1 of
       [TakeutiZaring] p. 88. $)
    en0 $p |- ( A ~~ (/) <-> A = (/) ) $=
      ( vf c0 cen wbr wceq cv wf1o wex 0ex bren ccnv f1ocnv f1o00 simprbi syl
      19.23aiv sylbi enref breq1 mpbiri impbii ) ACDEZACFZUCACBGZHZBIUDACBJKUFU
      DBUFCAUELZHZUDACUEMUHUGCFUDAUGNOPQRUDUCCCDECJSACCDTUAUB $.
      $( [27-May-1998] $)
  $}

  ${
    $d f A $.
    ensn1.1 $e |- A e. _V $.
    $( A singleton is equinumerous to ordinal one. $)
    ensn1 $p |- { A } ~~ 1o $=
      ( vf csn c0 c1o cen wbr cv wf1o wex cop 0ex f1osn snex f1oeq1 cla4ev
      ax-mp p0ex bren mpbir df1o2 breqtrri ) ADZEDZFGUDUEGHUDUECIZJZCKZUDUEAELZ
      DZJZUHAEBMNUGUKCUJUIOUDUEUFUJPQRUDUECSTUAUBUC $.
      $( [4-Nov-2002] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A singleton is equinumerous to ordinal one. $)
    ensn1g $p |- ( A e. B -> { A } ~~ 1o ) $=
      ( vx cv csn c1o cen wbr wceq sneq breq1d visset ensn1 vtoclg ) CDZEZFGHAE
      ZFGHCABOAIPQFGOAJKOCLMN $.
      $( [23-Apr-2004] $)
  $}

  ${
    $d x f A $.
    $( A set is equinumerous to ordinal one iff it is a singleton. $)
    en1 $p |- ( A ~~ 1o <-> E. x A = { x } ) $=
      ( vf c1o cen wbr cv csn wceq wex c0 wf1o df1o2 breq2i p0ex bren bitri
      ccnv cfv f1ocnv crn wfo f1ofo forn syl cop wf f1of wcel 0ex fsn2 simprbi
      rneqd fvex rnsnop syl6eq eqtr3d sneq eqeq2d cla4ev 3syl 19.23aiv sylbi
      visset ensn1 breq1 mpbiri impbii ) BDEFZBAGZHZIZAJZVIBKHZCGZLZCJZVMVIBVNE
      FVQDVNBEMNBVNCOPQVPVMCVPVNBVORZLZBKVRSZHZIZVMBVNVOTVSVRUAZBWAVSVNBVRUBWCB
      IVNBVRUCVNBVRUDUEVSWCKVTUFHZUAWAVSVRWDVSVNBVRUGZVRWDIZVNBVRUHWEVTBUIWFKBV
      RUJUKULUEUMKVTUJKVRUNZUOUPUQVLWBAVTWGVJVTIVKWABVJVTURUSUTVAVBVCVLVIAVLVIV
      KDEFVJAVDVEBVKDEVFVGVBVH $.
      $( [25-Jul-2004] $)
  $}

  ${
    $d x y f A $.
    2dom.1 $e |- A e. _V $.
    $( A set that dominates ordinal 2 has at least 2 different members. $)
    2dom $p |- ( 2o ~<_ A -> E. x e. A E. y e. A -. x = y ) $=
      ( vf c2o cdom wbr c0 csn cpr cv wf1 wex wceq wn wrex df2o2 breq1i brdom
      bitri cfv wcel wf ffvelrn f1f 0ex prid1 sylancl p0ex prid2 wne 0nep0
      df-ne mpbi wb f1fveq mpanr12 mtbiri eqeq1 notbid eqeq2 rcla42ev
      syl111anc 19.23aiv sylbi ) FCGHZIIJZKZCELZMZENZALZBLZOZPZBCQACQZVGVICGHVL
      FVICGRSVICEDTUAVKVQEVKIVJUBZCUCZVHVJUBZCUCZVRVTOZPZVQVICVJUDZIVIUCZVSVKVI
      CIVJUEVICVJUFZIVHUGUHZUIWDVHVIUCZWAVKVICVHVJUEWFIVHUJUKZUIVKWBIVHOZIVHULW
      JPUMIVHUNUOVKWEWHWBWJUPWGWIVICIVHVJUQURUSVPWCVRVNOZPABVRVTCCVMVROVOWKVMVR
      VNUTVAVNVTOWKWBVNVTVRVBVAVCVDVEVF $.
      $( [25-Jul-2004] $)
  $}

  ${
    $d x y z w F $.
    fundmen.1 $e |- F e. _V $.
    $( A function is equinumerous to its domain.  Exercise 4 of [Suppes]
       p. 98. $)
    fundmen $p |- ( Fun F -> dom F ~~ F ) $=
      ( vx vy vz vw wfun cdm cv cfv cop cint cvv wcel dmex a1i funfvop ex wrel
      wi funrel elreldm syl wa wceq wb wex cxp wss ssel2 df-rel sylib sylan
      elvv eqeq1 inteq inteqd visset op1stb syl6eq syl5bir opeq1 syl6 imp
      eqeq2 biimprcd adantl mpd ancoms eleq1d funopfv adantr sylbid exp32
      com24 imp43 opeq2d eqtr4d 19.23advv adantrl syl6req impbid1 en3d ) AGZCDA
      HZACIZWFAJZKZDIZLZLZWEMNWDABOPWDWFWENZWHANWFAQRWDASZWIANZWKWENZTAUAZWMWNW
      OAWIUBRUCWDWLWNUDZWFWKUEZWIWHUEZUFWDWQUDWRWSWDWNWRWSTZWLWDWNUDZWIEIZFIZKZ
      UEZFUGEUGZWTXAWIMMUHZNZXFAXGUIZWNXHWDAXGWIUJWDWMXIWPAUKULUMEFWIUNULXAXEWT
      EFXAXEWRWSXAXEWRUDZUDZWIWFXCKZWHXJWIXLUEZXAWRXEXMWRXEUDZXLXDUEZXMWRXEXOWR
      XEWFXBUEZXOWRXPWKXBUEXEWFWKXBUOXEWKXDLZLXBXEWJXQWIXDUPUQXBXCEURUSUTVAWFXB
      XCVBVCVDXEXOXMTWRXOXMXEXLXDWIVEVFVGVHZVIVGXKWGXCWFWDWNXEWRWGXCUEZWDWRXEWN
      XSWDWRXEWNXSTWDXNUDWNXLANZXSXNWNXTUFWDXNWIXLAXRVJVGWDXTXSTXNWFXCAFURVKVLV
      MVNVOVPVQVRVNVSVHVTWSWKWHLZLWFWSWJYAWIWHUPUQWFWGCURUSWAWBRWC $.
      $( [28-Jul-2004] $)
  $}

  $( A function is equinumerate to its domain.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  fndmeng $p |- ( ( F Fn A /\ A e. C ) -> A ~~ F ) $=
    ( wfn wcel wa cdm cen wbr cvv wfun fnex fnfun adantr wi c0 cif wceq funeq
    dmeq id breq12d imbi12d 0ex elimel fundmen dedth imp syl11anc wb fndm
    breq1d mpbid ) CADZABEZFZCGZCHIZACHIZUPCJEZCKZURABCLUNVAUOACMNUTVAURUTVAURO
    UTCPQZKZVBGZVBHIZOCPCVBRZVAVCURVECVBSVFUQVDCVBHCVBTVFUAUBUCVBCPJUDUEUFUGUHU
    IUNURUSUJUOUNUQACHACUKULNUM $.
    $( [22-Jun-2011] $)

  ${
    $d f y z w A $.  $d f y z w B $.
    mapsnen.1 $e |- A e. _V $.
    mapsnen.2 $e |- B e. _V $.
    $( Set exponentiation to a singleton exponent is equinumerous to its base.
       Exercise 4.43 of [Mendelson] p. 255. $)
    mapsnen $p |- ( A ^m { B } ) ~~ A $=
      ( vz vw vy csn cmap co cv cfv cop oprex cvv wcel fvex a1i snex wceq wa
      wex wrex mapsn abeq2i anbi1i r19.41v df-rex 3bitr2i fveq1 visset fvsn
      syl6eq eqeq2d equcom syl6bb pm5.32i anbi2i anass ancom exbii eleq1 opeq2
      sneqd anbi12d ceqsexv 3bitri en2 ) EFABHZIJZABEKZLZBFKZMZHZAVIINVLOPVKVJP
      ZBVKQRVOOPVMAPZVNSRVPVMVLTZUAZGKZAPZVKBVTMZHZTZVRUAZUAZGUBZVTVMTZWAWDUAZU
      AZGUBVQVKVOTZUAZVSWDGAUCZVRUAWEGAUCWGVPWMVRWMEVJGABECDUDUEUFWDVRGAUGWEGAU
      HUIWFWJGWFWAWDWHUAZUAWIWHUAWJWEWNWAWDVRWHWDVRVMVTTWHWDVLVTVMWDVLBWCLVTBVK
      WCUJBVTDGUKULUMUNFGUOUPUQURWAWDWHUSWIWHUTUIVAWIWLGVMFUKWHWAVQWDWKVTVMAVBW
      HWCVOVKWHWBVNVTVMBVCVDUNVEVFVGVH $.
      $( [17-Dec-2003] $)
  $}

  ${
    $d x y A $.
    map1.1 $e |- A e. _V $.
    $( Set exponentiation: ordinal 1 to any set is equinumerous to ordinal 1.
       Exercise 4.42(b) of [Mendelson] p. 255. $)
    map1 $p |- ( 1o ^m A ) ~~ 1o $=
      ( vx vy c1o cmap co c0 csn cxp oprex cvv wcel cv 0ex a1i p0ex xpex wceq
      wa ancom wf df1o2 opreq1i eleq2i elmap fconst2 3bitrri el1o anbi12i
      bitr2i en2 ) CDEAFGZEHAHIZJZEAFKHLMCNZUMMZOPUOLMDNZEMZAUNBQRPUSUPUOSZTUTU
      STUQURHSZTUSUTUAUTUQUSVAUQUPUNAFGZMAUNUPUBUTUMVBUPEUNAFUCUDUEUNAUPQBUFAHU
      POUGUHURUIUJUKUL $.
      $( [17-Dec-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Two singletons are equinumerous. $)
    en2sn $p |- ( ( A e. C /\ B e. D ) -> { A } ~~ { B } ) $=
      ( csn c1o cen wbr wcel entr ensn1g con0 wi 1on ensymg ax-mp syl syl2an )
      AEZFGHFBEZGHZSTGHACIBDIZSFTJACKUBTFGHZUABDKFLIUCUAMNTFLOPQR $.
      $( [9-Nov-2003] $)
  $}

  ${
    $d x A $.
    $( A singleton is finite. $)
    snfi $p |- { A } e. Fin $=
      ( vx csn cfn wcel cv cen wbr com wrex cvv c1o breq2 rcla4ev 1onn ensn1g
      sylancr wn c0 wceq snprc en0 peano1 mpan sylbir sylbi pm2.61i isfi mpbir
      ) ACZDEUJBFZGHZBIJZAKEZUMLIEUJLGHZUMUNULUOBLIUKLUJGMNOAKPQUNRUJSTZUMAUAUP
      UJSGHZUMUJUBSIEUQUMUCULUQBSIUKSUJGMNUDUEUFUGBUJUHUI $.
      $( [29-Aug-2008] $) $( [4-Nov-2002] $)
  $}

  ${
    $d x y $.
    $( The class of finite sets is a proper class.  (Contributed by Jeffrey
       Hankins, 10-Oct-2008.) $)
    fiprc $p |- Fin e/ _V $=
      ( vx vy cv csn wceq wex cab cvv wnel cfn snnex wcel wn wss snfi eleq1
      mpbiri 19.23aiv abssi ssexg mpan con3i df-nel 3imtr4i ax-mp ) ACZBCZDZEZB
      FZAGZHIZJHIZABKUKHLZMJHLZMULUMUOUNUKJNUOUNUJAJUIUFJLZBUIUPUHJLUGOUFUHJPQR
      SUKJHTUAUBUKHUCJHUCUDUE $.
      $( [3-Oct-2008] $)
  $}

  ${
    $d f g h A $.  $d f g h B $.  $d f g h C $.  $d f g h D $.
    $( Equinumerosity of union of disjoint sets.  Theorem 4 of [Suppes]
       p. 92. $)
    unen $p |- ( ( ( A ~~ B /\ C ~~ D ) /\
  ( ( A i^i C ) = (/) /\ ( B i^i D ) = (/) ) ) -> ( A u. C ) ~~ ( B u. D ) ) $=
      ( vf vg vh cun cvv wcel cen wbr wa cin c0 wceq wi cv wf1o wex wb unexb
      breng bi2anan9 sylbir f1oun visset unex f1oeq1 cla4ev syl syl5bir exp3a
      19.23advv eeanv syl5ibr sylbid imp3a wn brprc relen brrelexi anim12i
      sylib enrefg adantrd pm2.61i ) BDHZIJZABKLZCDKLZMZACNOPBDNOPMZMACHZVHKLZQ
      VIVLVMVOVIVLABERZSZETZCDFRZSZFTZMZVMVOQZVIBIJZDIJZMVLWBUABDUBWDVJVRWEVKWA
      ABIEUCCDIFUCUDUEVIVQVTMZFTETWCWBVIWFWCEFVIWFVMVOVIVOVNVHGRZSZGTZWFVMMZVNV
      HIGUCWJVNVHVPVSHZSZWIABCDVPVSUFWHWLGWKVPVSEUGFUGUHVNVHWGWKUIUJUKULUMUNVQV
      TEFUOUPUQURVIUSZVLVOVMWMVOVNVNKLZVLVNVHKUTVLVNIJZWNVLAIJZCIJZMWOVJWPVKWQA
      BKVAVBCDKVAVBVCACUBVDVNIVEUKULVFVG $.
      $( [11-Jun-1998] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    xpsnen.1 $e |- A e. _V $.
    xpsnen.2 $e |- B e. _V $.
    $( A set is equinumerous to its cross-product with a singleton.
       Proposition 4.22(c) of [Mendelson] p. 254. $)
    xpsnen $p |- ( A X. { B } ) ~~ A $=
      ( vy vx vz csn cxp cv cint cop snex xpex wcel wceq wa wex cvv elxp inteq
      inteqd visset op1stb syl6eq syl6eqel adantr 19.23aivv sylbi opex a1i wb
      eleq1 mpbii opeq1 eqeq2d anbi12d ceqsexgv ancom anass elsn anbi1i
      3bitr3i exbii opeq2 anbi1d ceqsexv syl6req pm4.71ri bitri 3bitri syl5bb
      syl pm5.32ri pm4.71i bitr2i en2 ) EFABHZIZAEJZKZKZFJZBLZAVRCBMNVTVSOZVTWC
      GJZLZPZWCAOZWFVROZQZQZGRZFRZWBSOZFGVTAVRTZWLWOFGWHWOWKWHWBWCSWHWBWGKZKWCW
      HWAWQVTWGUAUBWCWFFUCZUDUEWRUFUGUHUIWDSOWIWCBUJUKWEWCWBPZQVTWBBLZPZWBAOZQZ
      WSQZVTWDPZWIQZWIXEQWSWEXCWSWOWEXCULWSWCSOWOWRWCWBSUMUNWOWSXFQZFRZXCWEXFXC
      FWBSWSXEXAWIXBWSWDWTVTWCWBBUOUPWCWBAUMUQZURWEWNXHWPWMXGFWMWFBPZWHWIQZQZGR
      XFXGWLXLGXKWJQWJXKQWLXLXKWJUSWHWIWJUTWJXJXKGBVAVBVCVDXKXFGBDXJWHXEWIXJWGW
      DVTWFBWCVEUPVFVGXFWSXEQZWIQXGXEXMWIXEWSXEWBWDKZKWCXEWAXNVTWDUAUBWCBWRUDVH
      ZVIVBWSXEWIUTVJVKVDVJVLVMVNXFXFWSQXDXFWSXEWSWIXOUGVOWSXFXCXIVNVPXEWIUSVKV
      Q $.
      $( [4-Jan-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A set is equinumerous to its cross-product with a singleton.
       Proposition 4.22(c) of [Mendelson] p. 254. $)
    xpsneng $p |- ( ( A e. C /\ B e. D ) -> ( A X. { B } ) ~~ A ) $=
      ( vx vy cv csn cxp cen wbr wceq xpeq1 id breq12d sneq xpeq2 syl breq1d
      visset xpsnen vtocl2g ) EGZFGZHZIZUCJKAUEIZAJKABHZIZAJKEFABCDUCALZUFUGUCA
      JUCAUEMUJNOUDBLZUGUIAJUKUEUHLUGUILUDBPUEUHAQRSUCUDETFTUAUB $.
      $( [22-Oct-2004] $)
  $}


  ${
    $d x y A $.  $d x y B $.
    endisj.1 $e |- A e. _V $.
    endisj.2 $e |- B e. _V $.
    $( Any two sets are equinumerous to disjoint sets.  Exercise 4.39 of
       [Mendelson] p. 255. $)
    endisj $p |- E. x E. y ( ( x ~~ A /\ y ~~ B ) /\ ( x i^i y ) = (/) ) $=
      ( c0 csn cxp cen wbr c1o wa cin wceq cv wex 0ex xpsnen con0 1on elisseti
      pm3.2i xp01disj p0ex xpex snex breq1 bi2anan9 ineq12 eqeq1d anbi12d
      cla42ev mp2an ) CGHZIZCJKZDLHZIZDJKZMZUPUSNZGOZAPZCJKZBPZDJKZMZVDVFNZGOZM
      ZBQAQUQUTCGERSDLFLTUAUBSUCCDUDVKVAVCMABUPUSCUOEUEUFDURFLUGUFVDUPOZVFUSOZM
      ZVHVAVJVCVLVEUQVMVGUTVDUPCJUHVFUSDJUHUIVNVIVBGVDUPVFUSUJUKULUMUN $.
      $( [16-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
    undom.1 $e |- B e. _V $.
    undom.2 $e |- C e. _V $.
    undom.3 $e |- D e. _V $.
    $( Dominance law for union.  Proposition 4.24(a) of [Mendelson] p. 257. $)
    undom $p |- ( ( ( A ~<_ B /\ C ~<_ D ) /\ ( B i^i D ) = (/) ) ->
                ( A u. C ) ~<_ ( B u. D ) ) $=
      ( vx vy cdom wbr wa cin c0 wceq cun cdif wi cv cen wss wex unen undif2
      syl5eqbrr sseq2 ss0b syl6bb ss2in syl5bi imp difdisj jctil sylan2
      anassrs an32s unss12 cvv wcel unex ssdom2g ax-mp syl ad2antlr endomtr
      syl11anc ex an4s 19.23aiv 19.23adv domen syl2anb difss mp2 domtr mpan )
      ABJKZCDJKZLBDMZNOZACPZBDPZJKZVQCAQZDJKZVTWCRZVRAHSZTKZWGBUAZLZHUBZWDISZTK
      ZWLDUAZLZIUBZWFVQWEWKWPWFWKWOWFIWJWOWFRHWJWOWFWHWMWIWNWFWHWMLZWIWNLZLZVTW
      CWSVTLWAWGWLPZTKZWTWBJKZWCWQVTWRXAWQVTWRXAWQAWDMNOZWGWLMZNOZLZXAVTWRLZWQX
      FLAWDPWTWATAWGWDWLUCACUDUEXGXEXCVTWRXEVTXDVSUAZXEWRVTXHXDNUAXEVSNXDUFXDUG
      UHWGBWLDUIUJUKACULUMUNUOUPWRXBWQVTWRWTWBUAZXBWGBWLDUQWBURUSXIXBRBDEGUTWTW
      BURVAVBVCVDWAWTWBVEVFVGVHVGVIVJUKHABEVKIWDDGVKVLWDCJKZVRWECURUSWDCUAXJFCA
      VMWDCURVAVNWDCDVOVPUNUK $.
      $( [3-Sep-2004] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    xpcomen.1 $e |- A e. _V $.
    xpcomen.2 $e |- B e. _V $.
    $( Commutative law for equinumerosity of cross product.  Proposition
       4.22(d) of [Mendelson] p. 254. $)
    xpcomen $p |- ( A X. B ) ~~ ( B X. A ) $=
      ( vx vy vz vw cxp cv csn ccnv cuni xpex cvv wcel snex cnvex uniex a1i
      cop wceq wa wex sneq cnveq syl visset cnvsn syl6eq unieqd opex unisn
      syl6req eq2tri ancom anbi12i an32 3bitr4i 2exbii 19.41vv 3bitr3i elxp
      anbi1i excom bitri en2 ) EFABIZBAIZEJZKZLZMZFJZKZLZMZABCDNVMOPVJVHPZVLVKV
      JQRSTVQOPVNVIPZVPVOVNQRSTVJGJZHJZUAZUBZVTAPZWABPZUCZUCZHUDGUDZVNVMUBZUCZV
      NWAVTUAZUBZWEWDUCZUCZHUDGUDZVJVQUBZUCZVRWIUCVSWPUCWGWIUCZHUDGUDWNWPUCZHUD
      GUDWJWQWRWSGHWCWIUCZWFUCWLWPUCZWMUCWRWSWTXAWFWMVJVNWBWKVMVQWCVMWKKZMWKWCV
      LXBWCVLWBKZLZXBWCVKXCUBVLXDUBVJWBUEVKXCUFUGVTWAGUHZHUHZUIUJUKWKWAVTULUMUN
      WLVQXCMWBWLVPXCWLVPXBLZXCWLVOXBUBVPXGUBVNWKUEVOXBUFUGWAVTXFXEUIUJUKWBVTWA
      ULUMUNUOWDWEUPUQWCWFWIURWLWMWPURUSUTWGWIGHVAWNWPGHVAVBVRWHWIGHVJABVCVDVSW
      OWPVSWNGUDHUDWOHGVNBAVCWNHGVEVFVDUSVG $.
      $( [5-Jan-2004] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Commutative law for equinumerosity of cross product.  Proposition
       4.22(d) of [Mendelson] p. 254. $)
    xpcomeng $p |- ( ( A e. C /\ B e. D ) -> ( A X. B ) ~~ ( B X. A ) ) $=
      ( vx vy cv cxp cen wbr wceq xpeq1 xpeq2 breq12d visset xpcomen vtocl2g )
      EGZFGZHZSRHZIJASHZSAHZIJABHZBAHZIJEFABCDRAKTUBUAUCIRASLRASMNSBKUBUDUCUEIS
      BAMSBALNRSEOFOPQ $.
      $( [27-Mar-2006] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    xpassen.1 $e |- A e. _V $.
    xpassen.2 $e |- B e. _V $.
    xpassen.3 $e |- C e. _V $.
    $( Associative law for equinumerosity of cross product.  Proposition
       4.22(e) of [Mendelson] p. 254. $)
    xpassen $p |- ( ( A X. B ) X. C ) ~~ ( A X. ( B X. C ) ) $=
      ( vx vy vz vw vv vu cxp cv csn cdm cuni crn cop xpex cvv wcel opex a1i
      wceq wa wex sneq dmeqd unieqd sneqd op1sta sneqi dmeqi unieqi visset
      eqtri syl6req rneqd rneqi op2nda opeq12d eq2tri anass anbi12i an32
      3bitr4i exbii 19.41v 3bitr3i 2exbii 19.41vv elxp excom anbi1i an12
      exrot4 opeq1 eqeq2d anbi1d anbi2d ceqsexv 3bitri 3exbii anbi2i 19.42vv
      bitri bitr3i exrot3 opeq2 en2 ) GHABMZCMZABCMZMZGNZOZPZQZOZPZQZWTRZQZWQRZ
      QZSZSZHNZOZPZQZXJRZQZOZPZQZSZXORZQZSZWLCABDETFTXHUAUBWPWMUBZXBXGUCUDYAUAU
      BXIWOUBZXRXTUCUDWPINZJNZSZKNZSZUEZYDAUBZYEBUBZUFZYGCUBZUFZUFZKUGZJUGIUGZX
      IXHUEZUFZXIYDYEYGSZSZUEZYJYKYMUFZUFZUFZKUGZJUGZIUGZWPYAUEZUFZYBYRUFYCUUIU
      FYPYRUFZJUGIUGUUFUUIUFZJUGIUGYSUUJUUKUULIJYOYRUFZKUGUUEUUIUFZKUGUUKUULUUM
      UUNKYIYRUFZYNUFUUBUUIUFZUUDUFUUMUUNUUOUUPYNUUDWPXIYHUUAXHYAYIYDXBYTXGYIXB
      YHOZPZQZOZPZQZYDYIXAUVAYIWTUUTYIWSUUSYIWRUURYIWQUUQWPYHUHZUIUJUKZUIUJUVBY
      FOZPZQYDUVAUVFUUTUVEUUSYFYFYGYDYEUCZULUMZUNUOYDYEIUPZULUQURYIYEXDYGXFYIXD
      UUTRZQZYEYIXCUVJYIWTUUTUVDUSUJUVKUVERZQYEUVJUVLUUTUVEUVHUTUOYDYEUVIJUPZVA
      UQURYIXFUUQRZQYGYIXEUVNYIWQUUQUVCUSUJYFYGUVGKUPZVAURVBVBUUBYFXRYGXTUUBYDX
      LYEXQUUBXLUUAOZPZQYDUUBXKUVQUUBXJUVPXIUUAUHZUIUJYDYTUVIULURUUBXQUVPRZQZOZ
      PZQZYEUUBXPUWBUUBXOUWAUUBXNUVTUUBXMUVSUUBXJUVPUVRUSUJUKZUIUJUWCYTOZPZQYEU
      WBUWFUWAUWEUVTYTYDYTUVIYEYGUCZVAUMZUNUOYEYGUVMULUQURVBUUBXTUWARZQZYGUUBXS
      UWIUUBXOUWAUWDUSUJUWJUWERZQYGUWIUWKUWAUWEUWHUTUOYEYGUVMUVOVAUQURVBVCYJYKY
      MVDVEYIYNYRVFUUBUUDUUIVFVGVHYOYRKVIUUEUUIKVIVJVKYPYRIJVLUUFUUIIJVLVJYBYQY
      RYBWPLNZYGSZUEZUWLWLUBZYMUFUFZKUGLUGUWPLUGKUGZYQLKWPWLCVMUWPLKVNUWQUWLYFU
      EZYLUFZUWNYMUFZUFZJUGIUGZLUGKUGUXALUGZKUGJUGIUGYQUWPUXBKLUWOUWTUFUWSJUGIU
      GZUWTUFUWPUXBUWOUXDUWTIJUWLABVMVOUWNUWOYMVPUWSUWTIJVLVGVKUXAKLIJVQUXCYOIJ
      KUXCUWRYLUWTUFZUFZLUGYLYIYMUFZUFZYOUXAUXFLUWRYLUWTVDVHUXEUXHLYFUVGUWRUWTU
      XGYLUWRUWNYIYMUWRUWMYHWPUWLYFYGVRVSVTWAWBYLYIYMVPWCWDWCWCVOYCUUHUUIYCXIYD
      UWLSZUEZYJUWLWNUBZUFUFZLUGZIUGUUHILXIAWNVMUXMUUGIUXMUWLYTUEZUXJUUDUFZUFZK
      UGJUGZLUGUXPLUGZKUGJUGUUGUXLUXQLUXJYJUFZUXKUFUXSUXNUUCUFZKUGJUGZUFZUXLUXQ
      UXKUYAUXSJKUWLBCVMWEUXJYJUXKVDUYBUXSUXTUFZKUGJUGUXQUXSUXTJKWFUYCUXPJKUYCU
      XNUXSUUCUFZUFUXPUXSUXNUUCVPUYDUXOUXNUXJYJUUCVDWEWGVKWHVJVHUXPLJKWIUXRUUEJ
      KUXOUUELYTUWGUXNUXJUUBUUDUXNUXIUUAXIUWLYTYDWJVSVTWBVKWCVHWGVOVGWK $.
      $( [22-Jan-2004] $)
  $}

  ${
    $d t A $.  $d t x y z w v u f B $.  $d t x y z w v u f C $.
    xpdom.1 $e |- B e. _V $.
    xpdom.2 $e |- C e. _V $.
    $( Dominance law for cross product.  Proposition 10.33(2) of
       [TakeutiZaring] p. 92. $)
    xpdom2 $p |- ( A ~<_ B -> ( C X. A ) ~<_ ( C X. B ) ) $=
      ( vt vf vx vy vz vw vv vu cvv wcel cdom wbr cxp reldom brrelexi cv wi
      wceq breq1 xpeq2 breq1d imbi12d wf1 wex brdom visset xpex csn cdm cuni
      crn cfv cop wa wf f1f ffvelrn ex syl anim2d adantld elxp4 fvex opelxp
      3imtr4g wb wrex f1fveq ancoms anbi2d opth syl5bb ad2ant2l imp adantlr
      sneq dmeqd unieqd op1sta syl6eq rneqd op2nda fveq2d opeq12d eqeqan12d
      ad2antlr eqeq12 syl6bb 3bitr4d exp53 com23 r19.23aivv r19.23advv elxp2
      syl2anb com12 dom2d mpi 19.23aiv sylbi vtoclg mpcom ) ANOABPQZCARZCBRZPQZ
      ABPSTFUAZBPQZCXLRZXJPQZUBXHXKUBFANXLAUCZXMXHXOXKXLABPUDXPXNXIXJPXLACUEUFU
      GXMXLBGUAZUHZGUIXOXLBGDUJXRXOGXRXNNOXOCXLEFUKULXRHIXNXJHUAZUMZUNZUOZXTUPZ
      UOZXQUQZURZIUAZUMZUNZUOZYHUPZUOZXQUQZURZNXRXSYBYDURUCZYBCOZYDXLOZUSZUSYPY
      EBOZUSZXSXNOZYFXJOXRYRYTYOXRYQYSYPXRXLBXQUTZYQYSUBXLBXQVAUUBYQYSXLBYDXQVB
      VCVDVEVFXSCXLVGYBYECBYDXQVHVIVJUUAYGXNOZUSXRYFYNUCZXSYGUCZVKZXSJUAZKUAZUR
      ZUCZKXLVLJCVLZYGLUAZMUAZURZUCZMXLVLLCVLZXRUUFUBZUUAUUCUUKUUPUUQUUKUUOUUQL
      MCXLUUJUULCOZUUMXLOZUSZUUOUUQUBZUBJKCXLUUGCOZUUHXLOZUSZUUTUUJUVAUVDUUTUUJ
      UUOXRUUFUVDUUTUSZUUJUUOUSZUSXRUSUUGUUHXQUQZURZUULUUMXQUQZURZUCZUUGUULUCZU
      UHUUMUCZUSZUUDUUEUVEXRUVKUVNVKZUVFUVEXRUVOUVCUUSXRUVOUBUVBUURUVCUUSUSZXRU
      VOUVPXRUSZUVLUVGUVIUCZUSUVNUVKUVQUVRUVMUVLXRUVPUVRUVMVKXLBUUHUUMXQVMVNVOU
      UGUVGUULUVIJUKZUUHXQVHUUMXQVHVPVQVCVRVSVTUVFUUDUVKVKUVEXRUUJUUOYFUVHYNUVJ
      UUJYBUUGYEUVGUUJYBUUIUMZUNZUOUUGUUJYAUWAUUJXTUVTXSUUIWAZWBWCUUGUUHUVSWDWE
      UUJYDUUHXQUUJYDUVTUPZUOUUHUUJYCUWCUUJXTUVTUWBWFWCUUGUUHUVSKUKZWGWEWHWIUUO
      YJUULYMUVIUUOYJUUNUMZUNZUOUULUUOYIUWFUUOYHUWEYGUUNWAZWBWCUULUUMLUKZWDWEUU
      OYLUUMXQUUOYLUWEUPZUOUUMUUOYKUWIUUOYHUWEUWGWFWCUULUUMUWHMUKZWGWEWHWIWJWKU
      VFUUEUVNVKUVEXRUVFUUEUUIUUNUCUVNXSUUIYGUUNWLUUGUUHUULUUMUVSUWDUWJVPWMWKWN
      WOWPWQWRVSJKXSCXLWSLMYGCXLWSWTXAXBXCXDXEXFXG $.
      $( [29-Mar-2006] $) $( [24-Jul-2004] $)

    $( Dominance law for cross product.  Theorem 6L(c) of [Enderton] p. 149. $)
    xpdom1 $p |- ( A ~<_ B -> ( A X. C ) ~<_ ( B X. C ) ) $=
      ( cxp cdom wbr cen domentr cvv wcel xpcomeng reldom brrelexi sylancl
      xpdom2 endomtr syl11anc xpcomen ) ACFZCBFZGHZUBBCFZIHUAUDGHABGHZUAUBUDJUE
      UACAFZIHZUFUBGHUCAKLCKLUGUEACKKMABGNOEPABCDEQUAUFUBRSCBEDTP $.
      $( [29-Mar-2006] $) $( [28-Sep-2004] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d z C $.
    $( Dominance law for cross product.  Theorem 6L(c) of [Enderton] p. 149. $)
    xpdom1g $p |- ( ( B e. R /\ C e. S /\ A ~<_ B ) ->
                  ( A X. C ) ~<_ ( B X. C ) ) $=
      ( vy vz wcel cdom wbr cxp cv wi wceq breq2 xpeq1 breq2d imbi12d xpeq2
      breq12d imbi2d visset xpdom1 vtocl2g 3impia ) BDHCEHABIJZACKZBCKZIJZAFLZI
      JZAGLZKZUJULKZIJZMUFUMBULKZIJZMUFUIMFGBCDEUJBNZUKUFUOUQUJBAIOURUNUPUMIUJB
      ULPQRULCNZUQUIUFUSUMUGUPUHIULCASULCBSTUAAUJULFUBGUBUCUDUE $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    xpdom3.1 $e |- A e. _V $.
    $( A set is dominated by its cross product with a non-empty set.  Exercise
       6 of [Suppes] p. 98. $)
    xpdom3 $p |- ( B =/= (/) -> A ~<_ ( A X. B ) ) $=
      ( vx c0 wne cv wcel wex cxp cdom wbr n0 csn wss visset snss ssid xpss12
      mpan cvv wi snex xpex ssdomg ax-mp cen xpsnen ensymi endomtr 3syl sylbi
      19.23aiv ) BEFDGZBHZDIAABJZKLZDBMUOUQDUOUNNZBOZUQUNBDPZQUSAURJZUPOZVAUPKL
      ZUQAAOUSVBARAAURBSTVAUAHVBVCUBAURCUNUCUDVAUPUAUEUFAVAUGLVCUQVAACAUNCUTUHU
      IAVAUPUJTUKULUMUL $.
      $( [24-Nov-2006] $) $( [27-Jul-2004] $)
  $}

  ${
    $d x y z w v u A $.
    pw2en.1 $e |- A e. _V $.
    $( The power set of a set is equinumerous to set exponentiation with a base
       of ordinal 2.  Proposition 10.44 of [TakeutiZaring] p. 96.  (The proof
       seems excessively long.  An attempt to find a shorter one is on my to-do
       list.) $)
    pw2en $p |- ~P A ~~ ( 2o ^m A ) $=
      ( vx vy vz vw vv vu cpw c2o cmap co cv wcel c0 wceq wa cab copab ccnv
      csn cima pwex cvv opabex2 a1i visset cnvex imaexg ax-mp wss cin sseqin2
      biimpi eqeq1d cop eleq2 p0ex elimasn opelcnv bitri syl5bb wb wal eq2ab
      df-sn eqeq1i iba 19.21aiv 0ex eqeq1 anbi1d bibi12d cla4v wi eqid a1bi
      pm4.71 sylibr impbii 3bitr4ri anbi2i elin eleq1 anbi2d abbidv eqeq2d
      anbi12d opelopab 3bitr4i syl6rbbr eqrdv syl5cbi sseq1 crn imassrn cdm
      wfn eqeltrri simpl ss2abi ssexi fnopab2 fneq1 mpbiri fndm syl dfdm4
      syl5eqr sseq2d mpbii syl5cbir impbid elpw pm5.32ri ancom wf elsn abbi2dv
      cpr prid2 df2o2 eleqtrri syl6eqelr wn wne wex abn0 simpr 19.23aiv sylbi
      necon1bi prid1 syl6eqel pm2.61i fopab feq1 cfv wral syl6bb fnopfvb ffn
      sylan bicomd sylan9bb biimpa adantl eqtrd wo ffvelrn eleq2i fvex elpr
      sylib ord sylibrd con1d imp eqtr4d pm2.61dan fvopab2 mpan2 ad2antll expr
      r19.21aiv ax-17 hbopab1 eqfnfv2f sylancl mpbird ex impbid2 con0 2on
      elisseti elmap syl6bbr 3bitri en2 ) CDAIZJAKLZEMZANZFMZGMZOPZUWBCMZNZQZGR
      ZPZQZEFSZDMZTZOUAZUAZUBZABUCUWMUDNUWGUVTNZEFAUWJBUEUFUWRUDNZUWNUWANZUWOUD
      NUWTUWNDUGUHUWOUWQUDUIUJUFUWSUWNUWMPZQUWGUWRPZUXBQUXBUXCQUXAUXCQUXBUWSUXC
      UXBUWGAUKZUXCUWSUXBUXDUXCUXDAUWGULZUWRPUXCUXBUXDUXEUWGUWRUXDUXEUWGPUWGAUM
      UNUOUXBHUXEUWRUXBHMZUWRNZUXFUWPUPZUWMNZUXFUXENZUXBUXHUWNNZUXIUXGUWNUWMUXH
      UQUXGUWPUXFUPUWONUXKUWOUWPUXFURHUGZUSUWPUXFUWNURUXLUTVAVBUXFANZUXFUWGNZQU
      XMUWPUWFUXNQZGRZPZQZUXJUXIUXNUXQUXMUWFGRZUXPPUWFUXOVCZGVDZUXQUXNUWFUXOGVE
      UWPUXSUXPGOVFZVGUXNUYAUXNUXTGUXNUWFVHVIUYAOOPZUYCUXNQZVCZUXNUXTUYEGOVJUWF
      UWFUYCUXOUYDUWEOOVKZUWFUWFUYCUXNUYFVLVMVNUXNUYCUXNVOUYEUYCUXNOVPVQUYCUXNV
      RVAVSVTWAWBUXFAUWGWCUWLUXMUWDUXPPZQUXREFUXFUWPUXLURUWBUXFPZUWCUXMUWKUYGUW
      BUXFAWDUYHUWJUXPUWDUYHUWIUXOGUYHUWHUXNUWFUWBUXFUWGWDWEWFWGWHUWDUWPPUYGUXQ
      UXMUWDUWPUXPVKWEWIWJWKWLWMUXCUXDUWRAUKZUXBUWGUWRAWNUXBUWRUWOWOZUKUYIUWOUW
      QWPUXBUYJAUWRUXBUWNWQZAUYJUXBUWNAWRZUYKAPUXBUYLUWMAWRZEFAUWJUWMUWJUXSUWPU
      XSUDUYBURWSUWIUWFGUWFUWHWTXAXBZUWMVPZXCZAUWNUWMXDXEAUWNXFXGUWNXHXIXJXKXLX
      MUWGACUGXNVBXOUXCUXBXPUXCUXBUXAUXCUXBAJUWNXQZUXAUXCUXBUYQUXBUYQAJUWMXQEFA
      JUWJUWMUYOUWJJNZUWCUWHUYRUWHUWJUWPJUWHUWIGUWPUWHUWFUWIUWEUWPNUWHUWFVHGOXR
      VBXSZUWPOUWPXTZJOUWPURYAYBYCYDUWHYEZUWJOJUWHUWJOUWJOYFUWIGYGUWHUWIGYHUWIU
      WHGUWFUWHYIYJYKYLZOUYTJOUWPVJYMYBYCYNYOUFYPAJUWNUWMYQXEUXCUYQUXBUXCUYQQZU
      XBUWBUWNYRZUWBUWMYRZPZEAYSZVUCVUFEAUXCUYQUWCVUFUXCUYQUWCQZQZVUDUWJVUEVUIU
      WHVUDUWJPVUIUWHQVUDUWPUWJVUIUWHVUDUWPPZUXCUWHUWBUWPUPUWNNZVUHVUJUXCUWHUWB
      UWRNZVUKUWGUWRUWBUQVULUWPUWBUPUWONVUKUWOUWPUWBUREUGZUSUWPUWBUWNURVUMUTVAY
      TVUHVUJVUKUYLUWCVUJVUKVCUYQAUWBUWPUWNURUUAAJUWNUUBZUUCUUDUUEZUUFUWHUWPUWJ
      PVUIUYSUUGUUHVUIVUAQVUDOUWJVUIVUAVUDOPZVUIVUPUWHVUIVUPYEZVUJUWHVUHVUQVUJV
      OUXCVUHVUPVUJVUHVUDJNZVUPVUJUUIZAJUWBUWNUUJVURVUDUYTNVUSJUYTVUDYBUUKVUDOU
      WPUWBUWNUULUUMVAUUNUUOUUGVUOUUPUUQUURVUAUWJOPVUIVUBUUGUUSUUTUWCVUEUWJPZUX
      CUYQUWCUWJUDNVUTUYNEFAUWJUDUVAUVBUVCUUSUVDUVEUYQUXBVUGVCZUXCUYLUYMVVAUYQE
      HAUWNUWMUXFUWNNEUVFUWLEFHUVGUVHVUNUYPUVIUUGUVJUVKUVLJAUWNJUVMUVNUVOBUVPUV
      QXOUVRUVS $.
      $( [29-Jan-2004] $)
  $}

  ${
    $d f z B $.  $d f C $.  $d f ph $.  $d f x z $.
    ac6sfilem1.1 $e |- B e. _V $.
    ac6sfilem1.2 $e |- ( C = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Lemma for ~ ac6sfi . $)
    ac6sfilem1 $p |- ( C = ( B ` x ) -> ( ph <-> [ B / f ] ps ) ) $=
      ( vz cv cfv wceq wb wi wsbc cvv wcel wal ax-17 hbsbc1v hbbi hbim fveq1
      eqeq2d sbceq1a bibi2d imbi12d cla4gf ax-mp mpg ) ECJZFJZKZLZABMZNZEUKDKZL
      ZABFDOZMZNZFDPQUPFRVANGUPVAFIDPIJDQFSURUTFURFSAUSFAFSBFDGTUAUBULDLZUNURUO
      UTVBUMUQEUKULDUCUDVBBUSABFDUEUFUGUHUIHUJ $.
      $( [26-Jun-2009] $)
  $}

  ${
    ac6sfilem2.1 $e |- A e. _V $.
    $( Lemma for ~ ac6sfi . $)
    ac6sfilem2 $p |- ( F : suc A -1-1-onto-> B
         -> ( ( F " A ) u. { ( F ` A ) } ) = B ) $=
      ( csuc wf1o wfo wfn cima wceq wa cfv csn cun f1ofo fofn foima jca df-suc
      a1i imaeq2d imaundi wcel sucid fnsnfv eqcomd mpan2 uneq2d 3eqtrd eqeq1d
      biimpa 3syl ) AEZBCFUMBCGZCUMHZCUMIZBJZKCAIZACLMZNZBJZUMBCOUNUOUQUMBCPUMB
      CQRUOUQVAUOUPUTBUOUPCAAMZNZIZURCVBIZNZUTUOUMVCCUMVCJUOASTUAVDVFJUOCAVBUBT
      UOVEUSURUOAUMUCZVEUSJADUDUOVGKUSVEUMACUEUFUGUHUIUJUKUL $.
      $( [26-Jun-2009] $)
  $}

  ${
    $d f h m x z A $.  $d f g h m t w x y z B $.  $d f g h m t w z ph $.
    $d g h m t w y z ps $.  $d f g h k m t w x y z $.
    ac6sfi.1 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Lemma for ~ ac6sfi . $)
    ac6sfilem3 $p |- ( ( ( y e. B /\ [ ( `' h ` w ) / x ] ph /\ ( w e. om
            /\ `' h : suc w -1-1-onto-> z ) ) /\ ( g : ( `' h " w ) --> B
            /\ A. x e. ( `' h " w ) [ g / f ] ps ) ) -> ( ( g u. {
            <. ( `' h ` w ) , y >. } ) : z --> B /\ A. x e. z [ ( g u.
            { <. ( `' h ` w ) , y >. } ) / f ] ps ) ) $=
      ( vt vm cv wcel ccnv cfv wsbc com csuc wf1o wa w3a cima wf wral cop csn
      cun cin c0 wceq simprl fvex visset f1osn wss wi simpl1 snssd fss expcom
      syl f1of syl5 mpi wn wrex word nnord wne nordeq necom df-ne bitri sylib
      ex ad2antrl 3adant1 adantr imp wf1 wb f1of1 ad2antll ad2antrr elelsuc
      adantl sucid jctir f1fveq syl11anc mtbird nrexdv wfn fvelimab f1ofn
      sssucid sylancl disjsn sylibr fun syl21anc unidm a1i feq3 mpbid
      ac6sfilem2 feq2d ax-17 hbsbc1v hb3an hbra1 hban wo eqcomd eleq2d elun
      syl6bb pm2.27 simpl2 wfun cdm ffun ssun1 fdm 3ad2ant3 eleqtrrd funssfv
      syl111anc snex unex cvv wal ax-gen hbbi hbim eqeq1 sbceq1a bibi1d
      imbi12d cla4gf mp2 ac6sfilem1 eqcoms eqid ax-mp syl5rbbr mpbird 3exp1
      syld com4l impcom ra4 sylan2 fndm opex snid f1ofun funopfv jca fveq2
      eqeq1d ssun2 simpl syl6eqel simpr adantlr simplr eqtr2d biimpd exp31
      com34 com24 sylbird com12 ancoms 3ad2antl2 com13 sylbid com14 elsn
      syl5ib jaod r19.21ai jcai ) DNZGOZACFNZJNPZQZRZUWISOZUWITZENZUWJUAZUBZUCZ
      UWJUWIUDZGINZUEZBHUWTRZCUWSUFZUBZUBZUWOGUWTUWKUWGUGZUHZUIZUEZBHUXHRZCUWOU
      FZUXEUWSUWKUHZUIZGUXHUEZUXIUXEUXMGGUIZUXHUEZUXNUXEUXAUXLGUXGUEZUWSUXLUJUK
      ULZUXPUWRUXAUXCUMUXEUXLUWGUHZUXGUAZUXQUWKUWGUWIUWJUNZDUOZUPZUXEUXLUXSUXGU
      EZUXQUXTUXEUXSGUQZUYDUXQURUXEUWGGUWHUWLUWQUXDUSUTUYDUYEUXQUXLUXSGUXGVAVBV
      CUXLUXSUXGVDVEVFUXEUWKUWSOZVGUXRUXEUYFLNZUWJQUWKULZLUWIVHZUXEUYHLUWIUXEUY
      GUWIOZUBZUYHUYGUWIULZUXEUYJUYLVGZUWRUYJUYMURZUXDUWLUWQUYNUWHUWMUYNUWLUWPU
      WMUWIVIZUYNUWIVJUYOUYJUYMUYOUYJUBUWIUYGVKZUYMUWIUYGVLUYPUYGUWIVKUYMUWIUYG
      VMUYGUWIVNVOVPVQVCVRVSVTWAUYKUWNUWOUWJWBZUYGUWNOZUWIUWNOZUBUYHUYLWCUWRUYQ
      UXDUYJUWLUWQUYQUWHUWPUYQUWLUWMUWNUWOUWJWDWEVSWFUYKUYRUYSUYJUYRUXEUYGUWIWG
      WHUWIFUOZWIWJUWNUWOUYGUWIUWJWKWLWMWNUWJUWNWOZUWIUWNUQUYFUYIWCUXELUWNUWIUW
      KUWJWPUWRVUAUXDUWLUWQVUAUWHUWPVUAUWLUWMUWNUWOUWJWQWEVSVTUWIWRWSWMUWSUWKWT
      XAUWSUXLGGUWTUXGXBXCUXEUXOGULZUXPUXNWCVUBUXEGXDXEUXOGUXMUXHXFVCXGUXEUXMUW
      OGUXHUWRUXMUWOULZUXDUWLUWQVUCUWHUWPVUCUWLUWMUWIUWOUWJUYTXHZWEVSVTXIXGUXEU
      XIUXKUXEUXIUBZUXJCUWOUXEUXICUWRUXDCUWHUWLUWQCUWHCXJACUWKUYAXKUWQCXJXLUXAU
      XCCUXACXJUXBCUWSXMXNXNUXICXJXNVUECNZUWOOZVUFUWSOZVUFUXLOZXOZUXJVUEVUGVUFU
      XMOVUJVUEUWOUXMVUFUWRUWOUXMULZUXDUXIUWLUWQVUKUWHUWPVUKUWLUWMUWPUXMUWOVUDX
      PWEVSWFXQVUFUWSUXLXRXSVUEVUHUXJVUIUXEUXIVUHUXJURZUXDUXIVULURZUWRUXAVUHUXB
      URZVUMUXCVUNUXAVUMVUHVUNUXAUXIUXJVUHVUNUXBUXAUXIUXJURURVUHUXBXTVUHUXBUXAU
      XIUXJVUHUXBUXAUCZUXIUBZUXJUXBVUHUXBUXAUXIYAVUPVUFUXHQZVUFUWTQZULZUXJUXBWC
      VUPUXHYBZUWTUXHUQZVUFUWTYCZOVUSUXIVUTVUOUWOGUXHYDZWHVVAVUPUWTUXGYEXEVUPVU
      FUWSVVBVUHUXBUXAUXIUSVUOVVBUWSULZUXIUXAVUHVVDUXBUWSGUWTYFYGVTYHVUFUXHUWTY
      IYJVUSADVURRZUXJUXBVVEUXJWCVURVUQVVEBCUXHVURHUWTUXGIUOZUXFYKYLZVURYMOUWGV
      UFHNQZULZABWCZURZDYNVURVVHULZVVEBWCZURZVUFUWTUNZVVKDKYOVVKVVNDMVURYMMNVUR
      ODXJVVLVVMDVVLDXJVVEBDADVURVVOXKBDXJYPYQUWGVURULZVVIVVLVVJVVMUWGVURVVHYRV
      VPAVVEBADVURYSYTUUAUUBUUCZUUDUUEVURVURULVVEUXBWCVURUUFVVEBCUWTVURHVVFVVQU
      UDUUGUUHVCUUIUUJUUKUULUUMUXBCUWSUUNUUOWHWAVUEVUFUWKULZUXJVUIUXGYCZUXLULZU
      WKUXGQZUWGULZUBZVUEVVRUXJURURZUXTVWCUYCUXTVVTVWBUXTUXGUXLWOVVTUXLUXSUXGWQ
      UXLUXGUUPVCUXTUXFUXGOZVWBUXFUWKUWGUUQUURUXTUXGYBVWEVWBURUXLUXSUXGUUSUWKUW
      GUXGUYBUUTVCVFUVAUUGVVTVWBVWDVVRVWBVUEVVTUXJVVRVWBVUFUXGQZUWGULZVUEVVTUXJ
      URZURVVRVWAVWFUWGVVRVWFVWAVUFUWKUXGUVBXPUVCVUEVWGVVRVWHUXEUXIVWGVVRVWHURU
      RZUWLUWHUXDUXIVWIURZUWQUXDUWLVWJUXDUWLUBVVRVWGUXIVWHUWLVVRVWGUXIVWHURURZU
      RUXDVVRUWLVWKVVRUWLAVWKACUWKYSVVRUXIVWGAVWHVVRUXIVWGAVWHURURVVRUXIUBZVWGV
      VTAUXJVWLVWGVVTAUXJURZVWLVWGUBVVTUBZUWGVUQULZVWMVWNVUQVWFUWGVWNVUTUXGUXHU
      QZVUFVVSOZVUQVWFULVWLVUTVWGVVTUXIVUTVVRVVCWHWFVWPVWNUXGUWTUVDXEVWLVVTVWQV
      WGVVRVVTVWQUXIVVRVVTUBZVUFUXLVVSVWRVUFUWKUXLVVRVVTUVEUWKUYAUURUVFVVRVVTUV
      GYHUVHUVHVUFUXHUXGYIYJVWLVWGVVTUVIUVJVWOAUXJABCUXHUWGHVVGKUUDUVKVCUVLUVMV
      QUVNUVOUVPWHUVNUVQUVRWAUVSUVTUWAWAUUGCUWKUWBUWCUWDUVTUWEVQUWF $.
      $( [26-Jun-2009] $)

    $( A version of ~ ac6s for finite sets.  (Contributed by Jeffrey Hankins,
       26-Jun-2009.) $)
    ac6sfi $p |- ( ( A e. Fin /\ A. x e. A E. y e. B ph ) ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vm vh vz vw vt vk vg cfn wcel wrex wral cv wf wa wex cen wbr com wi
      isfi cvv relen brrelexi wf1o visset bren wal wceq f1oeq2 exbidv raleq
      feq2 anbi12d imbi12d cla4gv c0 csuc f1oeq3 imbi1d albidv ccnv f1ocnv
      f1o00 biimpi wsbc 0ex a1i f0 ral0 pm3.2i mpbiri adantl ax-17 hbsbc1v
      hbral hban feq1 sbceq1a ralbidv cla4egf sylc 3syl a1d 19.23aiv ax-gen
      cima cres wf1 wss f1ores f1of1 sssucid sylancl sylan2 cnvex resexg ax-mp
      f1oeq1 cla4ev pm2.27 crn dff1o5 simpr imassrn sseq2 mpbii ssralv imp
      sbequ12 cbvex cfv csn wfn sucid fnsnfv mpan2 eqcomd sseq1d biimpa sylan
      wfo f1ofo fofn cun foima df-suc imaeq2i imaundi eqtri eqeq1i ssun2 jca
      adantll w3a cop ac6sfilem3 snex unex syl 3exp1 r19.23aiv com12 wb fvex
      sbcrexg sylan2b ralsn syldan sylbi syl6 com3r mpd ex imaexg cla4v syl5
      com23 19.23adv cbvexv imbi1i albii 19.21aiv cbvalv syl5ib finds1 ) EPQZAD
      FRZCESZEFGTZUAZBCESZUBZGUCZUVNEITZUDUEZIUFRUVPUWAUGZIEUHUWCUWDIUFUWCUWBUF
      QZUWDUWCEUIQZUWEUWDUGZEUWBUDUJUKUWCEUWBJTZULZJUCZUWFUWGUGEUWBJIUMUNUWFUWE
      UWJUWDUWFKTZUWBUWHULZJUCZUVOCUWKSZUWKFUVQUAZBCUWKSZUBZGUCZUGZUGZKUOZUWJUW
      DUGZUWEUWTUXBKEUIUWKEUPZUWMUWJUWSUWDUXCUWLUWIJUWKEUWBUWHUQURUXCUWNUVPUWRU
      WAUVOCUWKEUSUXCUWQUVTGUXCUWOUVRUWPUVSUWKEFUVQUTBCUWKEUSVAURVBVBVCUXAUWKVD
      UWHULZJUCZUWSUGZKUOUWKLTZUWHULZJUCZUWSUGZKUOZUWKUXGVEZUWHULZJUCZUWSUGZKUO
      ZILUWBVDUPZUWTUXFKUXQUWMUXEUWSUXQUWLUXDJUWBVDUWKUWHVFURVGVHUWBUXGUPZUWTUX
      JKUXRUWMUXIUWSUXRUWLUXHJUWBUXGUWKUWHVFURVGVHUWBUXLUPZUWTUXOKUXSUWMUXNUWSU
      XSUWLUXMJUWBUXLUWKUWHVFURVGVHUXFKUXDUWSJUXDUWRUWNUXDVDUWKUWHVIZULZUXTVDUP
      ZUWKVDUPZUBZUWRUWKVDUWHVJUYAUYDUWKUXTVKVLUYDVDUIQZUWKFVDUAZBGVDVMZCUWKSZU
      BZUWRUYEUYDVNVOUYCUYIUYBUYCUYIVDFVDUAZUYGCVDSZUBUYJUYKFVPUYGCVQVRUYCUYFUY
      JUYHUYKUWKVDFVDUTUYGCUWKVDUSVAVSVTUWQUYIGCVDUICTZVDQGWAUYFUYHGUYFGWAUYGGC
      UWKUYLUWKQGWAZBGVDVNWBWCWDUVQVDUPZUWOUYFUWPUYHUWKFUVQVDWEUYNBUYGCUWKBGVDW
      FWGVAWHWIWJWKWLWMUXGUFQZMTZUXGUWHULZJUCZUVOCUYPSZUYPFUVQUAZBCUYPSZUBZGUCZ
      UGZUGZMUOZUXPUXKUYOVUFUXPUYOVUFUBUXOKUYOUYPUXGNTZULZNUCZVUDUGZMUOZUXOVUFU
      YOVUKUBUXMUWSJUYOVUKUXMUWSUGUYOUXMVUKUWSUYOUXMVUKUWSUGUYOUXMUBZUXTUXGWNZU
      XGVUGULZNUCZUVOCVUMSZVUMFUVQUAZBCVUMSZUBZGUCZUGZUGZUWSVUKVULVUOVVBUWSUGVU
      LUXGVUMUXTUXGWOZULZVUMUXGVVCVIZULZVUOUYOUXLUWKUXTULZVVDUXMVVGVVDUYOUXLUWK
      UXTWPZUXGUXLWQVVDVVGUXLUWKUXGUXTWRUXLUWKUXTWSUXGWTXAVTUWKUXLUWHVJZXBUXGVU
      MVVCVJVUNVVFNVVEVVCUXTUIQZVVCUIQUWHJUMXCZUXTUXGUIXDXEXCVUMUXGVUGVVEXFXGWJ
      VUOVVBVULUWSVUOVVBVVAVULUWSUGVUOVVAXHVULUWNVVAUWRVULUWNVVAUWRUGZVULUWNUBZ
      VUPVVLVULUWNVUPUXMUWNVUPUGZUYOUXMVVGVUMUWKWQZVVNVVIVVGVVHUXTXIZUWKUPZUBZV
      VQVVOVVGVVRUXLUWKUXTXJVLVVHVVQXKVVQVUMVVPWQVVOUXTUXGXLVVPUWKVUMXMXNWJUVOC
      VUMUWKXOWJVTXPVUPVVAVVMUWRVUPVVAVUTVVMUWRUGZVUPVUTXHVUTVUMFOTZUAZBGVVTVMZ
      CVUMSZUBZOUCVVSVUSVWDGOVUSOWAVWAVWCGVWAGWAVWBGCVUMUYLVUMQGWABGVVTOUMZWBWC
      WDUVQVVTUPZVUQVWAVURVWCVUMFUVQVVTWEVWFBVWBCVUMBGOXQWGVAXRVWDVVSOVVMVWDUWR
      VULUWNUVOCUXGUXTXSZXTZSZVWDUWRUGZUXMUWNVWIUYOUXTUXLYAZUXTUXGXTZWNZUWKWQZU
      BZUWNVWIUXMVWHUWKWQZUWNVWIVWOVWPUWNVWIUVOCVWHUWKXOXPVWKVWNVWPVWKVWMVWHUWK
      VWKVWHVWMVWKUXGUXLQVWHVWMUPUXGLUMYBUXLUXGUXTYCYDYEYFYGYHUXMVWKVWNUXMVVGUX
      LUWKUXTYIZVWKVVIUXLUWKUXTYJZUXLUWKUXTYKWJUXMUXTUXLWNZUWKUPZVUMVWMYLZUWKUP
      ZVWNUXMVVGVWQVWTVVIVWRUXLUWKUXTYMWJVWTVXBVWSVXAUWKVWSUXTUXGVWLYLZWNVXAUXL
      VXCUXTUXGYNYOUXTUXGVWLYPYQYRVLVXBVWMVXAWQVWNVWMVUMYSVXAUWKVWMXMXNWJYTYHUU
      AVULUVOCVWGVMZVWJVWIVULACVWGVMZDFRZVWJVXDVULVXFVWJUYOVVGVXFVWJUGUXMVXFUYO
      VVGUBZVWJVXEVXGVWJUGDFDTZFQZVXEVXGVWDUWRVXIVXEVXGUUBVWDUBUWKFVVTVWGVXHUUC
      ZXTZYLZUAZBGVXLVMZCUWKSZUBZUWRABCDKLFGOJHUUDVXLUIQVXPUWRUGVVTVXKVWEVXJUUE
      UUFZUWQVXPGMVXLUIUYPVXLQGWAVXMVXOGVXMGWAVXNGCUWKUYMBGVXLVXQWBWCWDUVQVXLUP
      ZUWOVXMUWPVXOUWKFUVQVXLWEVXRBVXNCUWKBGVXLWFWGVAWHXEUUGUUHUUIUUJVVIXBXPVWG
      UIQVXDVXFUUKUXGUXTUULZACDVWGFUIUUMXEUUNUVOCVWGVXSUUOUUNUUPUUJWLUUQUURUUSU
      UTUVAUUSUURUUSUUTVUJVVBMVUMVVJVUMUIQVVKUXTUXGUIUVBXEUYPVUMUPZVUIVUOVUDVVA
      VXTVUHVUNNUYPVUMUXGVUGUQURVXTUYSVUPVUCVUTUVOCUYPVUMUSVXTVUBVUSGVXTUYTVUQV
      UAVURUYPVUMFUVQUTBCUYPVUMUSVAURVBVBUVCUVDUVAUVEXPUVFVUEVUJMUYRVUIVUDUYQVU
      HJNUYPUXGUWHVUGXFUVGUVHUVIUUNUVJUVAUXJVUEKMUWKUYPUPZUXIUYRUWSVUDVYAUXHUYQ
      JUWKUYPUXGUWHUQURVYAUWNUYSUWRVUCUVOCUWKUYPUSVYAUWQVUBGVYAUWOUYTUWPVUAUWKU
      YPFUVQUTBCUWKUYPUSVAURVBVBUVKUVLUVMUVDUUSUUQUUTUUJUUIUUQXP $.
      $( [24-Jun-2009] $)
  $}


