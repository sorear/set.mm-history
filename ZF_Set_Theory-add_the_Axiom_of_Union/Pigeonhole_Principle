$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Pigeonhole Principle
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for Pigeonhole Principle.  If we join a natural number to itself
     minus an element, we end up with its successor minus the same element. $)
  phplem1 $p |- ( ( A e. om /\ B e. A ) ->
             ( { A } u. ( A \ { B } ) ) = ( suc A \ { B } ) ) $=
    ( com wcel wa csn cin c0 wceq cdif cun csuc word nnord wne nordeq disjsn2
    syl sylan undif4 df-suc uncom eqtri difeq1i syl6eqr ) ACDZBADZEAFZBFZGHIZUH
    AUIJKZALZUIJZIUFAMZUGUJANUNUGEABOUJABPABQRSUJUKUHAKZUIJUMUHAUITULUOUIULAUHK
    UOAUAAUHUBUCUDUER $.
    $( [25-May-1998] $)

  ${
    $d f A $.  $d f B $.
    phplem2.1 $e |- A e. _V $.
    phplem2.2 $e |- B e. _V $.
    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus one of its elements. $)
    phplem2 $p |- ( ( A e. om /\ B e. A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wa csn cdif cun csuc cen wbr cin c0 wceq cop wf1o f1osn snex
      f1oen ax-mp difss ssexi enref pm3.2i incom wss ssrin word nnord orddisj
      syl sseq2d mpbii ss0 syl5eq disjdif jctil unen sylancr adantr uncom
      difsnid adantl phplem1 3brtr3d ) AEFZBAFZGBHZAVJIZJZAHZVKJZAAKVJILVHVLVNL
      MZVIVHVJVMLMZVKVKLMZGVJVKNOPZVMVKNZOPZGVOVPVQVJVMBAQHZRVPBADCSVJVMWABTUAU
      BVKVKACAVJUCZUDUEUFVHVTVRVHVSVKVMNZOVMVKUGVHWCOUHZWCOPVHWCAVMNZUHZWDVKAUH
      WFWBVKAVMUIUBVHWEOWCVHAUJWEOPAUKAULUMUNUOWCUPUMUQVJAURUSVJVMVKVKUTVAVBVIV
      LAPVHVIVLVKVJJAVJVKVCABVDUQVEABVFVG $.
      $( [11-Jun-1998] $)

    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus any element of the successor. $)
    phplem3 $p |- ( ( A e. om /\ B e. suc A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( csuc wcel com wceq wo csn cdif cen wbr elsuci phplem2 wa enref word
      nnord orddif syl sneq difeq2d eqcoms sylan9eq syl5breq jaodan sylan2 ) BA
      EZFAGFZBAFZBAHZIAUIBJZKZLMZBANUJUKUOULABCDOUJULPAAUNLACQUJULAUIAJZKZUNUJA
      RAUQHASATUAUQUNHABABHUPUMUIABUBUCUDUEUFUGUH $.
      $( [26-May-1998] $)

    $( Lemma for Pigeonhole Principle.  Equinumerosity of successors implies
       equinumerosity of the original natural numbers. $)
    phplem4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A ~~ suc B -> A ~~ B ) ) $=
      ( vf csuc cen wbr cv wf1o wex com wcel wa sucex bren cfv csn cdif cima
      wf1 wss cres f1of1 sssucid jctir f1ores f1oen 3syl adantl word wceq
      nnord orddif imaeq2 wfn f1ofn sucid fnsnfv mpan2 difeq2 crn cdm imadmrn
      eqcomi a1i wfo f1ofo forn syl fndm 3eqtr3d difeq1d ccnv wfun dff1o3
      simprbi imadif 3eqtr4rd sylan9eq breqtrd fnfvelrn sylancl wb eleq2d
      mpbid fvex phplem3 sylan2 difss ssexi ensym entr syl2an anandirs ex
      19.23adv syl5bi ) AFZBFZGHWSWTEIZJZEKALMZBLMZNZABGHZWSWTEBDOZPXEXBXFEXEXB
      XFXCXDXBXFXCXBNZAWTAXAQZRZSZGHXKBGHZXFXDXBNZXHAXAATZXKGXBAXNGHZXCXBWSWTXA
      UAZAWSUBZNAXNXAAUCZJXOXBXPXQWSWTXAUDAUEUFWSWTAXAUGAXNXRCUHUIUJXCXBXNXAWSA
      RZSZTZXKXCAUKAXTULXNYAULAUMAUNAXTXAUOUIXBXAWSTZXJSZYBXAXSTZSZXKYAXBXAWSUP
      ZXJYDULZYCYEULWSWTXAUQZYFAWSMZYGACURZWSAXAUSUTXJYDYBVAUIXBWTYBXJXBXAVBZXA
      XAVCZTZWTYBYKYMULXBYMYKXAVDVEVFXBWSWTXAVGZYKWTULWSWTXAVHZWSWTXAVIZVJXBYFY
      LWSULYMYBULYHWSXAVKYLWSXAUOUIVLVMXBXAVNVOZYAYEULXBYNYQWSWTXAVPVQWSXSXAVRV
      JVSVTWAXMBXKGHZXLXBXDXIWTMZYRXBXIYKMZYSXBYFYIYTYHYJWSAXAWBWCXBYNYTYSWDYOY
      NYKWTXIYPWEVJWFBXIDAXAWGWHWIBXKXKWTXGWTXJWJWKWLVJAXKBWMWNWOWPWQWR $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Two equinumerous natural numbers are equal.  Proposition 10.20 of
       [TakeutiZaring] p. 90 and its converse.  Also compare Corollary 6E of
       [Enderton] p. 136. $)
    nneneq $p |- ( ( A e. om /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
      ( vz vx vy vw com wcel wa cen wbr wceq wi cv wral c0 csuc breq1 eqeq1
      imbi12d ralbidv visset ensym en0 eqcom bitri sylib a1i rgen wrex wo
      nn0suc wb breq2 eqeq2 bibi12d mpbiri biimpd ax-17 hbra1 hban ra4 phplem4
      imim1d ex a2d syl5 imp suceq syl8 biimprcd syl6 r19.23ad jaod syl7
      r19.21adv cbvralv syl6ib finds rcla4v impcom eqeng adantr impbid ) AGHZBG
      HZIABJKZABLZWFWEWGWHMZWEACNZJKZAWJLZMZCGOZWFWIDNZWJJKZWOWJLZMZCGOPWJJKZPW
      JLZMZCGOENZWJJKZXBWJLZMZCGOZXBQZWJJKZXGWJLZMZCGOZWNDEAWOPLZWRXACGXLWPWSWQ
      WTWOPWJJRWOPWJSTUAWOXBLZWRXECGXMWPXCWQXDWOXBWJJRWOXBWJSTUAWOXGLZWRXJCGXNW
      PXHWQXIWOXGWJJRWOXGWJSTUAWOALZWRWMCGXOWPWKWQWLWOAWJJRWOAWJSTUAXACGXAWJGHZ
      WSWJPJKZWTPWJCUBZUCXQWJPLWTWJUDWJPUEUFUGUHUIXBGHZXFXGFNZJKZXGXTLZMZFGOXKX
      SXFYCFGXTGHXTPLZXTWJQZLZCGUJZUKZXSXFYCCXTULXSXFYHYCMXSXFIZYDYCYGYDYCMYIYD
      YAYBYDYAYBUMXGPJKZXGPLZUMXGUDYDYAYJYBYKXTPXGJUNXTPXGUOUPUQURUHYIYFYCCGXSX
      FCXSCUSXECGUTVAYCCUSYIXPXGYEJKZXGYELZMZYFYCMYIXPYLXDYMXSXFXPYLXDMZMZXFXPX
      EMXSYPXECGVBXSXPXEYOXSXPXEYOMXSXPIYLXCXDXBWJEUBXRVCVDVEVFVGVHXBWJVIVJYFYC
      YNYFYAYLYBYMXTYEXGJUNXTYEXGUOTVKVLVMVNVEVOVPYCXJFCGXTWJLYAXHYBXIXTWJXGJUN
      XTWJXGUOTVQVRVSWMWICBGWJBLWKWGWLWHWJBAJUNWJBAUOTVTVGWAWEWHWGMWFABGWBWCWD
      $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Pigeonhole Principle.  A natural number is not equinumerous to a proper
       subset of itself.  Theorem (Pigeonhole Principle) of [Enderton] p. 134.
       The theorem is so-called because you can't put _n_ + 1 pigeons into _n_
       holes (if each hole holds only one pigeon).  The proof consists of
       lemmas ~ phplem1 through ~ phplem4 , ~ nneneq , and this final piece of
       the proof. $)
    php $p |- ( ( A e. om /\ B C. A ) -> -. A ~~ B ) $=
      ( vx vy com wcel wpss cen wbr wn wi wa cv csuc wceq wrex c0 wss 0ss
      sspsstr mpan wne 0pss df-ne bitri sylib nn0suc orcanai sylan2 cdom wex
      pssnel csn cdif pssss ssdif wb cin disjsn disj3 bitr3i sseq1 sylbi
      syl5ibr cvv visset sucex difss ssexi ssdom2g ax-mp syl56 imp phplem3
      ensym syl domentr syl2an exp43 com4r 19.23aiv mpcom endomtr sssucid mp2
      sbth sylancl expcom word peano2b nnord sucid nordeq nneneq sylanb anidms
      necon3bbid mpbird nsyli syli com12 psseq2 breq1 notbid imbi12d
      syl5ibrcom r19.23aiv ex pm2.43d ) AEFZBAGZABHIZJZXJXKXMXJXKXKXMKZXJXKLACM
      ZNZOZCEPZXNXKXJAQOZJZXRXKQAGZXTQBRXKYABSQBATUAYAAQUBXTAUCAQUDUEUFXJXSXRCA
      UGUHUIXQXNCEXOEFZXNXQBXPGZXPBHIZJZKYCYBYEYBYCBXOUJIZYEDMZXPFZYGBFJZLZDUKY
      CYBYFKZDBXPULYJYCYKKZDYHYIYLYIYCYBYHYFYIYCYBYHYFYIYCLBXPYGUMZUNZUJIZYNXOH
      IZYFYBYHLZYIYCYOYCBXPRZYIBYNRZYOBXPUOYRYSYIBYMUNZYNRZBXPYMUPYIBYTOZYSUUAU
      QYIBYMURQOUUBBYGUSBYMUTVABYTYNVBVCVDYNVEFYSYOKYNXPXOCVFZVGZXPYMVHVIZBYNVE
      VJVKVLVMYQXOYNHIYPXOYGUUCDVFVNXOYNUUEVOVPBYNXOVQVRVSVTVMWAWBYFYDXPXOHIZYB
      YDYFUUFYDYFLXPXOUJIXOXPUJIZUUFXPBXOWCXPVEFXOXPRUUGUUDXOWDXOXPVEVJWEXPXOWF
      WGWHYBUUFJXPXOUBZYBXPWIZXOXPFUUHYBXPEFZUUIXOWJZXPWKVCXOUUCWLXPXOWMWGYBUUF
      XPXOYBUUFXPXOOUQZYBUUJYBUULUUKXPXOWNWOWPWQWRWSWTXAXQXKYCXMYEAXPBXBXQXLYDA
      XPBHXCXDXEXFXGVPXHXIVM $.
      $( [29-May-1998] $)

    $( Corollary of Pigeonhole Principle. $)
    php2 $p |- ( ( A e. om /\ B C. A ) -> B ~< A ) $=
      ( vx com wcel wpss csdm wbr cv wa wi wceq eleq1 psseq2 anbi12d breq2
      imbi12d cdom cen wn cvv wss visset pssss ssdom2g mpsyl adantl php ensym
      nsyl brsdom sylanbrc vtoclg anabsi5 ) ADEZBAFZBAGHZCIZDEZBURFZJZBURGHZKUO
      UPJZUQKCADURALZVAVCVBUQVDUSUOUTUPURADMURABNOURABGPQVABURRHZBURSHZTVBUTVEU
      SURUAEUTBURUBVECUCZBURUDBURUAUEUFUGVAURBSHVFURBUHBURVGUIUJBURUKULUMUN $.
      $( [31-May-1998] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    $( Corollary of Pigeonhole Principle.  If ` A ` is finite and ` B ` is a
       proper subset of ` A ` , the ` B ` is strictly less numerous than
       ` A ` .  Stronger version of Corollary 6C of [Enderton] p. 135. $)
    php3 $p |- ( ( A e. Fin /\ B C. A ) -> B ~< A ) $=
      ( vx vf vy cfn wcel wpss csdm wbr cv cen com wrex wi isfi wa cdom wn cvv
      wss relen brrelexi pssss ssdom2g imp syl2an adantll wf1o wex visset bren
      cima imass2 syl adantl cdif c0 wceq pssnel eldif cfv wfun cdm f1ofun
      difss wfn wb f1ofn fndm sseq2 3syl mpbiri funfvima2 syl11anc ccnv wfo
      dff1o3 simprbi imadif eleq2d sylibd n0i syl6 syl5bir 19.23adv sylan2
      ssdif0 notbii sylibr dfpss3 sylanbrc crn imadmrn a1i imaeq2 f1ofo forn
      3eqtr3d psseq2d adantr mpbid php cres wf1 f1of1 f1ores resexg ax-mp
      f1oeq1 cla4ev imaexg entr expcom mtod exp32 syl5bi imp31 ex ensym
      syl6com ad2antlr brsdom exp31 r19.23aiv sylbi ) AFGZBAHZBAIJZYGACKZLJZCMN
      YHYIOZCAPYKYLCMYJMGZYKYHYIYMYKQYHQZBARJZBALJZSYIYKYHYOYMYKATGZBAUAZYOYHAY
      JLUBUCBAUDZYQYRYOBATUEUFUGUHYNYPYJBLJZYMYKYHYTSZYKAYJDKZUIZDUJYMYHUUAOZAY
      JDCUKZULYMUUCUUDDYMUUCYHUUAYMUUCYHQZQYTYJUUBBUMZLJZUUFYMUUGYJHZUUHSUUFUUG
      UUBAUMZHZUUIUUFUUGUUJUAZUUJUUGUAZSZUUKYHUULUUCYHYRUULYSBAUUBUNUOUPUUFUUJU
      UGUQZURUSZSZUUNYHUUCEKZAGUURBGSQZEUJZUUQEBAUTUUCUUTUUQUUCUUSUUQEUUSUURABU
      QZGZUUCUUQUURABVAUUCUVBUURUUBVBZUUOGZUUQUUCUVBUVCUUBUVAUMZGZUVDUUCUUBVCUV
      AUUBVDZUAZUVBUVFOAYJUUBVEUUCUVHUVAAUAZABVFUUCUUBAVGZUVGAUSZUVHUVIVHAYJUUB
      VIZAUUBVJZUVGAUVAVKVLVMUVAUURUUBVNVOUUCUVEUUOUVCUUCUUBVPVCZUVEUUOUSUUCAYJ
      UUBVQZUVNAYJUUBVRVSABUUBVTUOWAWBUUOUVCWCWDWEWFUFWGUUMUUPUUJUUGWHWIWJUUGUU
      JWKWLUUCUUKUUIVHYHUUCUUJYJUUGUUCUUBUVGUMZUUBWMZUUJYJUVPUVQUSUUCUUBWNWOUUC
      UVJUVKUVPUUJUSUVLUVMUVGAUUBWPVLUUCUVOUVQYJUSAYJUUBWQAYJUUBWRUOWSWTXAXBYJU
      UGXCWGUUFYTUUHOZYMUUFBUUGUUBBXDZUIZBUUGLJZUVRUUCAYJUUBXEYRUVTYHAYJUUBXFYS
      AYJBUUBXGUGUVTBUUGUURUIZEUJUWAUWBUVTEUVSUUBTGZUVSTGDUKZUUBBTXHXIBUUGUURUV
      SXJXKBUUGEUWCUUGTGUWDUUBBTXLXIULWJYTUWAUUHYJBUUGXMXNVLUPXOXPWFXQXRYKYPYTO
      YMYHYPYKBYJLJZYTYPYKUWEBAYJXMXSBYJUUEXTYAYBXOBAYCWLYDYEYFUF $.
      $( [22-Aug-2008] $)
  $}

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is strictly
     dominated by its successor. $)
  php4 $p |- ( A e. om -> A ~< suc A ) $=
    ( com wcel csuc wpss csdm wbr sucidg word wa wb nnord ordsuc biimpi ancli
    ordelpss 3syl mpbid peano2b php2 sylanb mpdan ) ABCZAADZEZAUDFGZUCAUDCZUEAB
    HUCAIZUHUDIZJUGUEKALUHUIUHUIAMNOAUDPQRUCUDBCUEUFASUDATUAUB $.
    $( [26-Jul-2004] $)

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is not
     equinumerous to its successor.  Corollary 10.21(1) of [TakeutiZaring]
     p. 90. $)
  php5 $p |- ( A e. om -> -. A ~~ suc A ) $=
    ( com wcel csuc csdm wbr cen wn php4 sdomnen syl ) ABCAADZEFALGFHAIALJK $.
    $( [26-Jul-2004] $)

