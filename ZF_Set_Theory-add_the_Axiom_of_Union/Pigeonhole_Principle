$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Pigeonhole Principle
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for Pigeonhole Principle.  If we join a natural number to itself
     minus an element, we end up with its successor minus the same element. $)
  phplem1 $p |- ( ( A e. om /\ B e. A ) ->
             ( { A } u. ( A \ { B } ) ) = ( suc A \ { B } ) ) $=
    ( com wcel wa csn cin c0 wceq cdif cun csuc word wne nordeq disjsn2 syl
    nnord sylan undif4 df-suc uncom eqtri difeq1i syl6eqr ) ACDZBADZEAFZBFZGHIZ
    UHAUIJKZALZUIJZIAMZUGUJUFUNUGEABNUJABOABPQARSUJUKUHAKZUIJUMUHAUITULUOUIULAU
    HKUOAUAAUHUBUCUDUEQ $.
    $( [25-May-1998] $)

  ${
    $d f A $.  $d f B $.
    phplem2.1 $e |- A e. _V $.
    phplem2.2 $e |- B e. _V $.
    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus one of its elements. $)
    phplem2 $p |- ( ( A e. om /\ B e. A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wa csn cdif cun csuc cen wbr cin c0 wceq unen cop wf1o f1osn
      snex f1oen ax-mp difss ssexi enref pm3.2i wss ssrin word nnord orddisj
      syl sseq2d mpbii ss0 incom syl5eq difdisj jctil sylancr adantr difsnid
      uncom adantl phplem1 3brtr3d ) AEFZBAFZGBHZAVJIZJZAHZVKJZAAKVJILVHVLVNLMZ
      VIVJVMLMZVKVKLMZGVJVKNOPZVMVKNZOPZGVOVHVJVMVKVKQVPVQVJVMBARHZSVPBADCTVJVM
      WABUAUBUCVKVKACAVJUDZUEUFUGVHVTVRVHVKVMNZOVSVHWCOUHZWCOPVHWCAVMNZUHZWDVKA
      UHWFWBVKAVMUIUCVHWEOWCVHAUJWEOPAUKAULUMUNUOWCUPUMVMVKUQURVJAUSUTVAVBVIVLA
      PVHVIVKVJJAVLABVCVJVKVDURVEABVFVG $.
      $( [11-Jun-1998] $)

    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus any element of the successor. $)
    phplem3 $p |- ( ( A e. om /\ B e. suc A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wceq wo csuc csn cdif cen wbr phplem2 wa word nnord orddif
      syl sneq difeq2d eqcoms sylan9eq enref syl5breq jaodan elsuci sylan2 ) AE
      FZBAFZBAGZHAAIZBJZKZLMZBULFUIUJUOUKABCDNUIUKOAUNALUIUKAULAJZKZUNUIAPAUQGA
      QARSUQUNGABABGUPUMULABTUAUBUCACUDUEUFBAUGUH $.
      $( [26-May-1998] $)

    $( Lemma for Pigeonhole Principle.  Equinumerosity of successors implies
       equinumerosity of the original natural numbers. $)
    phplem4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A ~~ suc B -> A ~~ B ) ) $=
      ( vf com wcel wa csuc cv wf1o wex cen wbr cfv csn cdif entr cima wf1 wss
      cres f1of1 sssucid jctir f1ores f1oen 3syl adantl word wceq nnord orddif
      imaeq2 wfn f1ofn sucid fnsnfv mpan2 difeq2 crn cdm imadmrn eqcomi a1i
      wfo f1ofo forn syl fndm 3eqtr3d difeq1d ccnv wfun dff1o3 simprbi imadif
      3eqtr4rd sylan9eq breqtrd fvex phplem3 fnfvelrn sylancl wb eleq2d mpbid
      sylan2 sucex difss ssexi ensym syl2an anandirs ex 19.23adv bren syl5ib )
      AFGZBFGZHZAIZBIZEJZKZELABMNZXBXCMNXAXEXFEXAXEXFWSWTXEXFAXCAXDOZPZQZMNXIBM
      NZXFWSXEHZWTXEHZAXIBRXKAXDASZXIMXEAXMMNZWSXEXBXCXDTZAXBUAZHAXMXDAUBZKXNXE
      XOXPXBXCXDUCAUDUEXBXCAXDUFAXMXQCUGUHUIWSXEXMXDXBAPZQZSZXIWSAUJAXSUKXMXTUK
      AULAUMAXSXDUNUHXEXDXBSZXHQZYAXDXRSZQZXIXTXEXDXBUOZXHYCUKZYBYDUKXBXCXDUPZY
      EAXBGZYFACUQZXBAXDURUSXHYCYAUTUHXEXCYAXHXEXDVAZXDXDVBZSZXCYAYJYLUKXEYLYJX
      DVCVDVEXEXBXCXDVFZYJXCUKXBXCXDVGZXBXCXDVHZVIXEYEYKXBUKYLYAUKYGXBXDVJYKXBX
      DUNUHVKVLXEXDVMVNZXTYDUKXEYMYPXBXCXDVOVPXBXRXDVQVIVRVSVTXLBXIMNZXJWTXGXCG
      ZYQXEBXGDAXDWAWBXEXGYJGZYRYEYHYSXEXBAXDWCYGYIWDXEYMYSYRWEYNYMYJXCXGYOWFVI
      WGWHBXIXIXCBDWIZXCXHWJWKWLVIWMWNWOWPXBXCEYTWQWR $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Two equinumerous natural numbers are equal.  Proposition 10.20 of
       [TakeutiZaring] p. 90 and its converse.  Also compare Corollary 6E of
       [Enderton] p. 136. $)
    nneneq $p |- ( ( A e. om /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
      ( vz vx vy vw com wcel wa cen wbr wceq wi cv wral breq2 eqeq2 imbi12d
      rcla4v c0 csuc breq1 eqeq1 ralbidv visset ensym en0 eqcom bitri sylib
      a1i rgen wrex wo wb bibi12d mpbiri biimpd ax-17 hbra1 hban phplem4
      imim1d ex a2d ra4 syl5 imp suceq syl8 biimprcd syl6 r19.23ad jaod nn0suc
      syl7 r19.21adv cbvralv syl6ib finds impcom eqeng adantr impbid ) AGHZBGHZ
      IABJKZABLZWFWEWGWHMZWFACNZJKZAWJLZMZCGOZWIWEWMWICBGWJBLWKWGWLWHWJBAJPWJBA
      QRSDNZWJJKZWOWJLZMZCGOTWJJKZTWJLZMZCGOENZWJJKZXBWJLZMZCGOZXBUAZWJJKZXGWJL
      ZMZCGOZWNDEAWOTLZWRXACGXLWPWSWQWTWOTWJJUBWOTWJUCRUDWOXBLZWRXECGXMWPXCWQXD
      WOXBWJJUBWOXBWJUCRUDWOXGLZWRXJCGXNWPXHWQXIWOXGWJJUBWOXGWJUCRUDWOALZWRWMCG
      XOWPWKWQWLWOAWJJUBWOAWJUCRUDXACGXAWJGHZWSWJTJKZWTTWJCUEZUFXQWJTLWTWJUGWJT
      UHUIUJUKULXBGHZXFXGFNZJKZXGXTLZMZFGOXKXSXFYCFGXSXFXTTLZXTWJUAZLZCGUMZUNZY
      CXTGHXSXFYHYCMXSXFIZYDYCYGYDYCMYIYDYAYBYDYAYBUOXGTJKZXGTLZUOXGUGYDYAYJYBY
      KXTTXGJPXTTXGQUPUQURUKYIYFYCCGXSXFCXSCUSXECGUTVAYCCUSYIXPXGYEJKZXGYELZMZY
      FYCMYIXPYLXDYMXSXFXPYLXDMZMZXSXPXEMYPXFXSXPXEYOXSXPXEYOMXSXPIYLXCXDXBWJEU
      EXRVBVCVDVEXECGVFVGVHXBWJVIVJYFYCYNYFYAYLYBYMXTYEXGJPXTYEXGQRVKVLVMVNVDCX
      TVOVPVQYCXJFCGXTWJLYAXHYBXIXTWJXGJPXTWJXGQRVRVSVTVGWAWEWHWGMWFABGWBWCWD
      $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Pigeonhole Principle.  A natural number is not equinumerous to a proper
       subset of itself.  Theorem (Pigeonhole Principle) of [Enderton] p. 134.
       The theorem is so-called because you can't put _n_ + 1 pigeons into _n_
       holes (if each hole holds only one pigeon).  The proof consists of
       lemmas ~ phplem1 through ~ phplem4 , ~ nneneq , and this final piece of
       the proof. $)
    php $p |- ( ( A e. om /\ B C. A ) -> -. A ~~ B ) $=
      ( vx vy com wcel wpss cen wbr wn wi wa cv csuc wceq wrex c0 nn0suc
      orcanai wss 0ss sspsstr mpan wne 0pss df-ne bitri sylib sylan2 psseq2
      breq1 notbid imbi12d cdom wex pssnel csn cdif domentr wb cin disjsn
      disj3 bitr3i sseq1 sylbi ssdif syl5bir cvv visset sucex difss ssexi
      ssdom2g ax-mp syl6 pssss syl5 imp phplem3 ensym syl syl2an exp43 com4r
      19.23aiv mpcom sbth endomtr sssucid mp2 sylancl expcom word nordeq
      peano2b nnord sucid nneneq sylanb anidms necon3bbid mpbird nsyli syli
      com12 syl5cbir r19.23aiv ex pm2.43d ) AEFZBAGZABHIZJZXKXLXNXKXLXLXNKZXKXL
      LACMZNZOZCEPZXOXKAQOZJZXSXLXKXTXSCARSXLQAGZYAQBTXLYBBUAQBAUBUCYBAQUDYAAUE
      AQUFUGUHUIXRXOCEXRXOBXQGZXQBHIZJZKXPEFZXRXLYCXNYEAXQBUJXRXMYDAXQBHUKULUMY
      CYFYEYFYCBXPUNIZYEDMZXQFZYHBFJZLZDUOYCYFYGKZDBXQUPYKYCYLKZDYIYJYMYJYCYFYI
      YGYJYCYFYIYGBXQYHUQZURZUNIZYOXPHIZYGYJYCLYFYILZBYOXPUSYJYCYPYJBXQTZYPYCYJ
      YSBYOTZYPYJYTBYNURZYOTZYSYJBUUAOZYTUUBUTYJBYNVAQOUUCBYHVBBYNVCVDBUUAYOVEV
      FBXQYNVGVHYOVIFYTYPKYOXQXPCVJZVKZXQYNVLVMZBYOVIVNVOVPBXQVQVRVSYRXPYOHIYQX
      PYHUUDDVJVTXPYOUUFWAWBWCWDWEVSWFWGYGYDXQXPHIZYFYDYGUUGXQXPUNIXPXQUNIZUUGY
      DYGLXQXPWHXQBXPWIXQVIFXPXQTUUHUUEXPWJXPXQVIVNWKWLWMYFUUGJXQXPUDZXQWNZXPXQ
      FUUIYFXQXPWOYFXQEFZUUJXPWPZXQWQVFXPUUDWRWLYFUUGXQXPYFUUGXQXPOUTZUUKYFUUMY
      FXQXPWSUULWTXAXBXCXDXEXFXGXHWBXIXJVS $.
      $( [29-May-1998] $)

    $( Corollary of Pigeonhole Principle. $)
    php2 $p |- ( ( A e. om /\ B C. A ) -> B ~< A ) $=
      ( vx com wcel wpss csdm wbr cv wa wi wceq eleq1 psseq2 anbi12d breq2
      imbi12d cdom cen wn brsdom wss pssss cvv visset ssdom2g ax-mp syl adantl
      php ensym nsyl sylanbrc vtoclg anabsi5 ) ADEZBAFZBAGHZCIZDEZBUSFZJZBUSGHZ
      KUPUQJZURKCADUSALZVBVDVCURVEUTUPVAUQUSADMUSABNOUSABGPQVCBUSRHZBUSSHZTVBBU
      SUAVAVFUTVABUSUBZVFBUSUCUSUDEVHVFKCUEZBUSUDUFUGUHUIVBUSBSHVGUSBUJBUSVIUKU
      LUMUNUO $.
      $( [31-May-1998] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    $( Corollary of Pigeonhole Principle.  If ` A ` is finite and ` B ` is a
       proper subset of ` A ` , the ` B ` is strictly less numerous than
       ` A ` .  Stronger version of Corollary 6C of [Enderton] p. 135. $)
    php3 $p |- ( ( A e. Fin /\ B C. A ) -> B ~< A ) $=
      ( vx vf vy cfn wcel wpss csdm wbr cv cen com wrex wi isfi cdom wn wa
      brsdom cvv wss ssdom2g imp relen brrelexi pssss syl2an adantll wf1o wex
      cima php dfpss3 imass2 syl adantl cdif c0 wceq cfv wfun cdm f1ofun difss
      wfn wb f1ofn fndm sseq2 3syl mpbiri funfvima2 syl11anc ccnv wfo dff1o3
      simprbi imadif eleq2d sylibd n0i syl6 eldif syl5ibr 19.23adv pssnel
      sylan2 ssdif0 notbii sylibr sylanbrc crn imadmrn a1i imaeq2 f1ofo forn
      3eqtr3d psseq2d adantr mpbid cres wf1 f1ores f1of1 visset resexg ax-mp
      f1oeq1 cla4ev imaexg bren entr expcom mtod exp32 syl5ib imp31 ex ensym
      syl6com ad2antlr exp31 r19.23aiv sylbi ) AFGZBAHZBAIJZYGACKZLJZCMNYHYIOZC
      APYKYLCMYJMGZYKYHYIYIBAQJZBALJZRYMYKSYHSZBATYKYHYNYMAUAGZBAUBZYNYKYHYQYRY
      NBAUAUCUDAYJLUEUFBAUGZUHUIYPYOYJBLJZYMYKYHYTRZYMAYJDKZUJZDUKYHUUAOZYKYMUU
      CUUDDYMUUCYHUUAYMUUCYHSZSYTYJUUBBULZLJZYMUUFYJHZUUGRUUEYJUUFUMUUEUUFUUBAU
      LZHZUUHUUJUUFUUIUBZUUIUUFUBZRZUUEUUFUUIUNYHUUKUUCYHYRUUKYSBAUUBUOUPUQUUEU
      UIUUFURZUSUTZRZUUMUUCEKZAGUUQBGRSZEUKZUUPYHUUCUUSUUPUUCUURUUPEUUCUUQABURZ
      GZUUPUURUUCUVAUUQUUBVAZUUNGZUUPUUCUVAUVBUUBUUTULZGZUVCUUCUUBVBUUTUUBVCZUB
      ZUVAUVEOAYJUUBVDUUCUVGUUTAUBZABVEUUCUUBAVFZUVFAUTZUVGUVHVGAYJUUBVHZAUUBVI
      ZUVFAUUTVJVKVLUUTUUQUUBVMVNUUCUVDUUNUVBUUCUUBVOVBZUVDUUNUTUUCAYJUUBVPZUVM
      AYJUUBVQVRABUUBVSUPVTWAUUNUVBWBWCUUQABWDWEWFUDEBAWGWHUULUUOUUIUUFWIWJWKWL
      UUCUUJUUHVGYHUUCUUIYJUUFUUCUUBUVFULZUUBWMZUUIYJUVOUVPUTUUCUUBWNWOUUCUVIUV
      JUVOUUIUTUVKUVLUVFAUUBWPVKUUCUVNUVPYJUTAYJUUBWQAYJUUBWRUPWSWTXAXBWHUUEYTU
      UGOZYMUUEBUUFUUBBXCZUJZBUUFLJZUVQAYJUUBXDYRUVSUUCYHAYJBUUBXEAYJUUBXFYSUHU
      VSBUUFUUQUJZEUKUVTUWAUVSEUVRUUBUAGZUVRUAGDXGZUUBBUAXHXIBUUFUUQUVRXJXKBUUF
      EUWBUUFUAGUWCUUBBUAXLXIXMWKYTUVTUUGYJBUUFXNXOVKUQXPXQWFAYJDCXGZXMXRXSYKYO
      YTOYMYHYOYKBYJLJZYTYOYKUWEBAYJXNXTBYJUWDYAYBYCXPWLYDYEYFUD $.
      $( [22-Aug-2008] $)
  $}

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is strictly
     dominated by its successor. $)
  php4 $p |- ( A e. om -> A ~< suc A ) $=
    ( com wcel csuc wpss csdm wbr sucidg word wa wb nnord ordsuc biimpi ancli
    ordelpss 3syl mpbid php2 peano2b sylanb mpdan ) ABCZAADZEZAUDFGZUCAUDCZUEAB
    HUCAIZUHUDIZJUGUEKALUHUIUHUIAMNOAUDPQRUDBCUEUFUCUDASATUAUB $.
    $( [26-Jul-2004] $)

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is not
     equinumerous to its successor.  Corollary 10.21(1) of [TakeutiZaring]
     p. 90. $)
  php5 $p |- ( A e. om -> -. A ~~ suc A ) $=
    ( com wcel csuc csdm wbr cen wn php4 sdomnen syl ) ABCAADZEFALGFHAIALJK $.
    $( [26-Jul-2004] $)

