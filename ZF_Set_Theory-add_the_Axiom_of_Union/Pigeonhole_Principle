$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Pigeonhole Principle
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for Pigeonhole Principle.  If we join a natural number to itself
     minus an element, we end up with its successor minus the same element. $)
  phplem1 $p |- ( ( A e. om /\ B e. A ) ->
             ( { A } u. ( A \ { B } ) ) = ( suc A \ { B } ) ) $=
    ( com wcel wa csn cin c0 wceq cdif cun csuc word nnord nordeq disjsn2 sylan
    wne syl undif4 df-suc uncom eqtri difeq1i syl6eqr ) ACDZBADZEAFZBFZGHIZUHAU
    IJKZALZUIJZIUFAMZUGUJANUNUGEABRUJABOABPSQUJUKUHAKZUIJUMUHAUITULUOUIULAUHKUO
    AUAAUHUBUCUDUES $.
    $( [25-May-1998] $)

  ${
    $d f A $.  $d f B $.
    phplem2.1 $e |- A e. _V $.
    phplem2.2 $e |- B e. _V $.
    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus one of its elements. $)
    phplem2 $p |- ( ( A e. om /\ B e. A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wa csn cdif cun csuc cen wbr cin c0 wceq ax-mp wss syl syl5eq
      cop wf1o f1osn snex f1oen difss ssexi enref pm3.2i incom ssrin word nnord
      orddisj sseq2d mpbii ss0 disjdif jctil unen sylancr adantr difsnid adantl
      uncom phplem1 3brtr3d ) AEFZBAFZGBHZAVJIZJZAHZVKJZAAKVJILVHVLVNLMZVIVHVJV
      MLMZVKVKLMZGVJVKNOPZVMVKNZOPZGVOVPVQVJVMBAUAHZUBVPBADCUCVJVMWABUDUEQVKVKA
      CAVJUFZUGUHUIVHVTVRVHVSVKVMNZOVMVKUJVHWCORZWCOPVHWCAVMNZRZWDVKARWFWBVKAVM
      UKQVHWEOWCVHAULWEOPAUMAUNSUOUPWCUQSTVJAURUSVJVMVKVKUTVAVBVIVLAPVHVIVLVKVJ
      JAVJVKVEABVCTVDABVFVG $.
      $( [11-Jun-1998] $)

    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus any element of the successor. $)
    phplem3 $p |- ( ( A e. om /\ B e. suc A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( csuc wcel com wceq wo csn cdif cen wbr elsuci phplem2 enref word orddif
      wa nnord syl sneq difeq2d eqcoms sylan9eq syl5breq jaodan sylan2 ) BAEZFA
      GFZBAFZBAHZIAUIBJZKZLMZBANUJUKUOULABCDOUJULSAAUNLACPUJULAUIAJZKZUNUJAQAUQ
      HATARUAUQUNHABABHUPUMUIABUBUCUDUEUFUGUH $.
      $( [26-May-1998] $)

    $( Lemma for Pigeonhole Principle.  Equinumerosity of successors implies
       equinumerosity of the original natural numbers. $)
    phplem4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A ~~ suc B -> A ~~ B ) ) $=
      ( vf csuc cen wbr cv wf1o com wcel wa csn cdif cima 3syl wceq imaeq2 syl
      wex sucex bren cfv wf1 cres f1of1 sssucid jctir f1ores f1oen adantl nnord
      wss orddif wfn f1ofn sucid fnsnfv mpan2 difeq2 crn cdm imadmrn eqcomi a1i
      word wfo f1ofo forn fndm 3eqtr3d difeq1d ccnv wfun dff1o3 imadif 3eqtr4rd
      simprbi sylan9eq breqtrd fnfvelrn sylancl eleq2d mpbid fvex phplem3 difss
      wb sylan2 ssexi ensym entr syl2an anandirs ex exlimdv syl5bi ) AFZBFZGHWS
      WTEIZJZEUAAKLZBKLZMZABGHZWSWTEBDUBZUCXEXBXFEXEXBXFXCXDXBXFXCXBMZAWTAXAUDZ
      NZOZGHXKBGHZXFXDXBMZXHAXAAPZXKGXBAXNGHZXCXBWSWTXAUEZAWSUNZMAXNXAAUFZJXOXB
      XPXQWSWTXAUGAUHUIWSWTAXAUJAXNXRCUKQULXCXBXNXAWSANZOZPZXKXCAVGAXTRXNYARAUM
      AUOAXTXASQXBXAWSPZXJOZYBXAXSPZOZXKYAXBXAWSUPZXJYDRZYCYERWSWTXAUQZYFAWSLZY
      GACURZWSAXAUSUTXJYDYBVAQXBWTYBXJXBXAVBZXAXAVCZPZWTYBYKYMRXBYMYKXAVDVEVFXB
      WSWTXAVHZYKWTRWSWTXAVIZWSWTXAVJZTXBYFYLWSRYMYBRYHWSXAVKYLWSXASQVLVMXBXAVN
      VOZYAYERXBYNYQWSWTXAVPVSWSXSXAVQTVRVTWAXMBXKGHZXLXBXDXIWTLZYRXBXIYKLZYSXB
      YFYIYTYHYJWSAXAWBWCXBYNYTYSWIYOYNYKWTXIYPWDTWEBXIDAXAWFWGWJBXKXKWTXGWTXJW
      HWKWLTAXKBWMWNWOWPWQWR $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Two equinumerous natural numbers are equal.  Proposition 10.20 of
       [TakeutiZaring] p. 90 and its converse.  Also compare Corollary 6E of
       [Enderton] p. 136. $)
    nneneq $p |- ( ( A e. om /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
      ( vz vy vw com wcel cen wceq wi cv wral breq1 eqeq1 imbi12d ralbidv breq2
      wbr c0 eqeq2 vx wa csuc visset ensym en0 eqcom bitri a1i rgen wrex nn0suc
      sylib wo wb bibi12d mpbiri biimpd ax-17 hbra1 hban ra4 phplem4 imim1d a2d
      ex syl5 imp suceq syl8 biimprcd syl6 rexlimd jaod ralrimdv cbvralv syl6ib
      syl7 finds rcla4v impcom eqeng adantr impbid ) AFGZBFGZUBABHRZABIZWFWEWGW
      HJZWEACKZHRZAWJIZJZCFLZWFWIUAKZWJHRZWOWJIZJZCFLSWJHRZSWJIZJZCFLDKZWJHRZXB
      WJIZJZCFLZXBUCZWJHRZXGWJIZJZCFLZWNUADAWOSIZWRXACFXLWPWSWQWTWOSWJHMWOSWJNO
      PWOXBIZWRXECFXMWPXCWQXDWOXBWJHMWOXBWJNOPWOXGIZWRXJCFXNWPXHWQXIWOXGWJHMWOX
      GWJNOPWOAIZWRWMCFXOWPWKWQWLWOAWJHMWOAWJNOPXACFXAWJFGZWSWJSHRZWTSWJCUDZUEX
      QWJSIWTWJUFWJSUGUHUMUIUJXBFGZXFXGEKZHRZXGXTIZJZEFLXKXSXFYCEFXTFGXTSIZXTWJ
      UCZIZCFUKZUNZXSXFYCCXTULXSXFYHYCJXSXFUBZYDYCYGYDYCJYIYDYAYBYDYAYBUOXGSHRZ
      XGSIZUOXGUFYDYAYJYBYKXTSXGHQXTSXGTUPUQURUIYIYFYCCFXSXFCXSCUSXECFUTVAYCCUS
      YIXPXGYEHRZXGYEIZJZYFYCJYIXPYLXDYMXSXFXPYLXDJZJZXFXPXEJXSYPXECFVBXSXPXEYO
      XSXPXEYOJXSXPUBYLXCXDXBWJDUDXRVCVDVFVEVGVHXBWJVIVJYFYCYNYFYAYLYBYMXTYEXGH
      QXTYEXGTOVKVLVMVNVFVRVOYCXJECFXTWJIYAXHYBXIXTWJXGHQXTWJXGTOVPVQVSWMWICBFW
      JBIWKWGWLWHWJBAHQWJBATOVTVGWAWEWHWGJWFABFWBWCWD $.
      $( [28-May-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Pigeonhole Principle.  A natural number is not equinumerous to a proper
       subset of itself.  Theorem (Pigeonhole Principle) of [Enderton] p. 134.
       The theorem is so-called because you can't put _n_ + 1 pigeons into _n_
       holes (if each hole holds only one pigeon).  The proof consists of
       lemmas ~ phplem1 through ~ phplem4 , ~ nneneq , and this final piece of
       the proof. $)
    php $p |- ( ( A e. om /\ B C. A ) -> -. A ~~ B ) $=
      ( vx vy com wcel wpss cen wbr wn wi wa cv wceq c0 wss wne cdom cvv imp wb
      csuc wrex 0ss sspsstr mpan df-ne bitri sylib nn0suc orcanai sylan2 pssnel
      0pss wex csn cdif pssss ssdif cin disjsn disj3 bitr3i sseq1 sylbi syl5ibr
      visset sucex difss ssexi ssdom2g ax-mp syl56 phplem3 ensym domentr syl2an
      syl exp43 com4r exlimiv mpcom endomtr sssucid sbth sylancl expcom peano2b
      word nnord sucid nordeq nneneq sylanb anidms necon3bbid mpbird nsyli syli
      mp2 com12 psseq2 breq1 notbid imbi12d syl5ibrcom rexlimiv ex pm2.43d ) AE
      FZBAGZABHIZJZXJXKXMXJXKXKXMKZXJXKLACMZUBZNZCEUCZXNXKXJAONZJZXRXKOAGZXTOBP
      XKYABUDOBAUEUFYAAOQXTAUNAOUGUHUIXJXSXRCAUJUKULXQXNCEXOEFZXNXQBXPGZXPBHIZJ
      ZKYCYBYEYBYCBXORIZYEDMZXPFZYGBFJZLZDUOYCYBYFKZDBXPUMYJYCYKKZDYHYIYLYIYCYB
      YHYFYIYCYBYHYFYIYCLBXPYGUPZUQZRIZYNXOHIZYFYBYHLZYIYCYOYCBXPPZYIBYNPZYOBXP
      URYRYSYIBYMUQZYNPZBXPYMUSYIBYTNZYSUUAUAYIBYMUTONUUBBYGVABYMVBVCBYTYNVDVEV
      FYNSFYSYOKYNXPXOCVGZVHZXPYMVIVJZBYNSVKVLVMTYQXOYNHIYPXOYGUUCDVGVNXOYNUUEV
      OVRBYNXOVPVQVSVTTWAWBYFYDXPXOHIZYBYDYFUUFYDYFLXPXORIXOXPRIZUUFXPBXOWCXPSF
      XOXPPUUGUUDXOWDXOXPSVKWTXPXOWEWFWGYBUUFJXPXOQZYBXPWIZXOXPFUUHYBXPEFZUUIXO
      WHZXPWJVEXOUUCWKXPXOWLWFYBUUFXPXOYBUUFXPXONUAZYBUUJYBUULUUKXPXOWMWNWOWPWQ
      WRWSXAXQXKYCXMYEAXPBXBXQXLYDAXPBHXCXDXEXFXGVRXHXIT $.
      $( [29-May-1998] $)

    $( Corollary of Pigeonhole Principle. $)
    php2 $p |- ( ( A e. om /\ B C. A ) -> B ~< A ) $=
      ( vx com wcel wpss csdm wbr cv wa wceq eleq1 psseq2 anbi12d breq2 imbi12d
      wi cdom cen cvv wn wss visset pssss ssdom2g mpsyl adantl php ensym brsdom
      nsyl sylanbrc vtoclg anabsi5 ) ADEZBAFZBAGHZCIZDEZBURFZJZBURGHZQUOUPJZUQQ
      CADURAKZVAVCVBUQVDUSUOUTUPURADLURABMNURABGOPVABURRHZBURSHZUAVBUTVEUSURTEU
      TBURUBVECUCZBURUDBURTUEUFUGVAURBSHVFURBUHBURVGUIUKBURUJULUMUN $.
      $( [31-May-1998] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    $( Corollary of Pigeonhole Principle.  If ` A ` is finite and ` B ` is a
       proper subset of ` A ` , the ` B ` is strictly less numerous than
       ` A ` .  Stronger version of Corollary 6C of [Enderton] p. 135. $)
    php3 $p |- ( ( A e. Fin /\ B C. A ) -> B ~< A ) $=
      ( vx vf vy wcel wpss wbr cv cen com wi wa wn cvv wss imp wf1o cima wceq
      cfn csdm wrex isfi relen brrelexi pssss ssdom2g syl2an adantll wex visset
      cdom bren imass2 syl adantl cdif c0 pssnel eldif cfv cdm f1ofun difss wfn
      wfun wb f1ofn fndm 3syl mpbiri funfvima2 syl11anc ccnv wfo dff1o3 simprbi
      sseq2 imadif eleq2d sylibd n0i syl6 syl5bir exlimdv sylan2 ssdif0 sylnibr
      dfpss3 sylanbrc crn imadmrn a1i imaeq2 f1ofo 3eqtr3d psseq2d adantr mpbid
      forn php wf1 f1of1 f1ores resexg ax-mp f1oeq1 cla4ev imaexg sylibr expcom
      cres entr mtod exp32 syl5bi imp31 ex ensym ad2antlr brsdom exp31 rexlimiv
      syl6com sylbi ) AUAFZBAGZBAUBHZYGACIZJHZCKUCYHYILZCAUDYKYLCKYJKFZYKYHYIYM
      YKMYHMZBAUMHZBAJHZNYIYKYHYOYMYKAOFZBAPZYOYHAYJJUEUFBAUGZYQYRYOBAOUHQUIUJY
      NYPYJBJHZYMYKYHYTNZYKAYJDIZRZDUKYMYHUUALZAYJDCULZUNYMUUCUUDDYMUUCYHUUAYMU
      UCYHMZMYTYJUUBBSZJHZUUFYMUUGYJGZUUHNUUFUUGUUBASZGZUUIUUFUUGUUJPZUUJUUGPZN
      UUKYHUULUUCYHYRUULYSBAUUBUOUPUQUUFUUJUUGURZUSTZUUMYHUUCEIZAFUUPBFNMZEUKZU
      UONZEBAUTUUCUURUUSUUCUUQUUSEUUQUUPABURZFZUUCUUSUUPABVAUUCUVAUUPUUBVBZUUNF
      ZUUSUUCUVAUVBUUBUUTSZFZUVCUUCUUBVGUUTUUBVCZPZUVAUVELAYJUUBVDUUCUVGUUTAPZA
      BVEUUCUUBAVFZUVFATZUVGUVHVHAYJUUBVIZAUUBVJZUVFAUUTVSVKVLUUTUUPUUBVMVNUUCU
      VDUUNUVBUUCUUBVOVGZUVDUUNTUUCAYJUUBVPZUVMAYJUUBVQVRABUUBVTUPWAWBUUNUVBWCW
      DWEWFQWGUUJUUGWHWIUUGUUJWJWKUUCUUKUUIVHYHUUCUUJYJUUGUUCUUBUVFSZUUBWLZUUJY
      JUVOUVPTUUCUUBWMWNUUCUVIUVJUVOUUJTUVKUVLUVFAUUBWOVKUUCUVNUVPYJTAYJUUBWPAY
      JUUBXAUPWQWRWSWTYJUUGXBWGUUFYTUUHLZYMUUFBUUGUUBBXMZRZBUUGJHZUVQUUCAYJUUBX
      CYRUVSYHAYJUUBXDYSAYJBUUBXEUIUVSBUUGUUPRZEUKUVTUWAUVSEUVRUUBOFZUVROFDULZU
      UBBOXFXGBUUGUUPUVRXHXIBUUGEUWBUUGOFUWCUUBBOXJXGUNXKYTUVTUUHYJBUUGXNXLVKUQ
      XOXPWFXQXRYKYPYTLYMYHYPYKBYJJHZYTYPYKUWDBAYJXNXSBYJUUEXTYEYAXOBAYBWKYCYDY
      FQ $.
      $( [22-Aug-2008] $)
  $}

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is strictly
     dominated by its successor. $)
  php4 $p |- ( A e. om -> A ~< suc A ) $=
    ( com wcel csuc wpss csdm wbr sucidg word wa wb nnord ordsuc ancli ordelpss
    biimpi 3syl mpbid peano2b php2 sylanb mpdan ) ABCZAADZEZAUDFGZUCAUDCZUEABHU
    CAIZUHUDIZJUGUEKALUHUIUHUIAMPNAUDOQRUCUDBCUEUFASUDATUAUB $.
    $( [26-Jul-2004] $)

  $( Corollary of the Pigeonhole Principle ~ php : a natural number is not
     equinumerous to its successor.  Corollary 10.21(1) of [TakeutiZaring]
     p. 90. $)
  php5 $p |- ( A e. om -> -. A ~~ suc A ) $=
    ( com wcel csuc csdm wbr cen wn php4 sdomnen syl ) ABCAADZEFALGFHAIALJK $.
    $( [26-Jul-2004] $)

