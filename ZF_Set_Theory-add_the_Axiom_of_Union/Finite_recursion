$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Finite recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( We use the next 3 theorems for finite recursion.  Note that we take
     advantage of what we have done for transfinite recursion, which allows
     us to explicitly specify the function and moreover not require the
     existence of omega. $)

  $( The function generated by finite recursive definition generation is a
     function on omega. $)
  frfnom $p |- ( rec ( F , A ) |` om ) Fn om $=
    ( crdg com cres wfn wfun cdm wceq df-fn con0 rdgfnon fnfun ax-mp funres
    cin fndm ineq2i dmres wss omsson dfss mpbi 3eqtr4i mpbir2an ) BACZDEZDFUGGZ
    UGHZDIUGDJUFGZUHUFKFZUJABLZKUFMNDUFONDUFHZPDKPZUIDUMKDUKUMKIULKUFQNRUFDSDKT
    DUNIUADKUBUCUDUE $.
    $( [15-Oct-1996] $)

  $( The initial value resulting from finite recursive definition
     generation. $)
  fr0g $p |- ( A e. B -> ( ( rec ( F , A ) |` om ) ` (/) ) = A ) $=
    ( wcel c0 crdg cfv com cres rdg0g wceq peano1 fvres ax-mp syl5eq ) ABDECAFZ
    GZAEPHIGZABCJEHDRQKLEHPMNO $.
    $( [15-Oct-1996] $)

  $( The successor value resulting from finite recursive definition
     generation. $)
  frsuc $p |- ( B e. om -> ( ( rec ( F , A ) |` om ) ` suc B ) =
              ( F ` ( ( rec ( F , A ) |` om ) ` B ) ) ) $=
    ( com wcel csuc crdg cfv cres con0 wceq nnon rdgsuc syl peano2b fvres
    sylbi fveq2d 3eqtr4d ) BDEZBFZCAGZHZBUBHZCHZUAUBDIZHZBUFHZCHTBJEUCUEKBLABCM
    NTUADEUGUCKBOUADUBPQTUHUDCBDUBPRS $.
    $( [15-Oct-1996] $)

  ${
    $d z D $.  $d y z C $.  $d z A $.  $d z B $.  $d x y z $.
    frsucopab.1 $e |- ( z e. A -> A. x z e. A ) $.
    frsucopab.2 $e |- ( z e. B -> A. x z e. B ) $.
    frsucopab.3 $e |- ( z e. D -> A. x z e. D ) $.
    frsucopab.4 $e |- F = ( rec ( { <. x , y >. | y = C } , A ) |` om ) $.
    frsucopab.5 $e |- ( x = ( F ` B ) -> C = D ) $.
    $( The successor value resulting from finite recursive definition
       generation (special case where the generation function is an ordered
       pair abstraction). $)
    frsucopab $p |- ( ( B e. om /\ D e. R ) -> ( F ` suc B ) = D ) $=
      ( com wcel csuc cfv cv wceq copab crdg cres frsuc fveq1i syl5eq cvv fvex
      hbopab1 hbrdg ax-17 hbres hbfv eqeq2i sylbir fvopabgf mpan sylan9eq ) EOP
      ZGHPZEQZIRZEBSFTZABUAZDUBZOUCZRZVDRZGUSVAVFRVHVBDEVDUDVAIVFMUEUFVGUGPUTVH
      GTEVFUHABCVGFGUGHACEVFACVEOACDVDVCABCUIJUJCSOPAUKULKUMLASZVGTVIEIRZTFGTVJ
      VGVIEIVFMUEUNNUOUPUQUR $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d z D $.  $d y z C $.  $d z A $.  $d z B $.  $d x y z $.
    frsucmpt.1 $e |- ( z e. A -> A. x z e. A ) $.
    frsucmpt.2 $e |- ( z e. B -> A. x z e. B ) $.
    frsucmpt.3 $e |- ( z e. D -> A. x z e. D ) $.
    frsucmpt.4 $e |- F = ( rec ( ( x e. _V |-> C ) , A ) |` om ) $.
    frsucmpt.5 $e |- ( x = ( F ` B ) -> C = D ) $.
    $( The successor value resulting from finite recursive definition
       generation (special case where the generation function is expressed in
       maps-to notation). $)
    frsucmpt $p |- ( ( B e. om /\ D e. R ) -> ( F ` suc B ) = D ) $=
      ( vy cvv cmpt crdg com cres cv wceq copab wcel wa df-mpt visset biantrur
      opabbii eqtr4i rdgeq1 ax-mp reseq1 eqtri frsucopab ) ANBCDEFGHIJKHAOEPZCQ
      ZRSZNTEUAZANUBZCQZRSZLUPUTUAZUQVAUAUOUSUAVBUOATOUCZURUDZANUBUSANOEUEURVDA
      NVCURAUFUGUHUICUOUSUJUKUPUTRULUKUMMUN $.
      $( [2-Nov-2011] $)
  $}

  ${
    $d x y z w F $.  $d x y z w A $.
    tz7.48.1 $e |- F Fn On $.
    $( A way of showing an ordinal function is one-to-one. $)
    tz7.48lem $p |- ( ( A C_ On /\ A. x e. A A. y e. x
                    -. ( F ` x ) = ( F ` y ) ) -> Fun `' ( F |` A ) ) $=
      ( vw vz con0 wss cv cfv wceq wn wral wa cres wi ccnv wfun wcel wal wb
      fvres eqeqan12d ad2antrl ssel anim12d wo pm3.48 oridm eqcom notbii
      orbi1i bitr3i syl6ibr con2d word ordtri3 biimprd eloni syl2an syl9r syl6
      imp32 sylbid exp32 a2d alimdv r2al 3imtr4g pm3.26 anim1i imim1i exp3a
      2alimi 3imtr4i eleq1 fveq2 eqeq2d notbid imbi12d cbvralv ralbii eleq2
      eqeq1d ralbidv bitri 3bitri ralcom2 sylbi ancri r19.26-2 sylibr syl syl5
      imdistani wfn cvv wf wf1 dff13 df-f1 pm3.27bi dffn2 sylanb fnssres mpan
      sylan ) CHIZAJZDKZBJZDKZLZMZBXJNACNZOXIXJDCPZKZXLXQKZLZXJXLLZQZBCNACNZOXQ
      RSZXIXPYCXIXJXLTZXMXKLZMZQZXLXJTZXOQZOZBCNACNZYCXPXIXJCTZXLCTZOZYKQZBUAZA
      UAYOYBQZBUAZAUAYLYCXIYQYSAXIYPYRBXIYOYKYBXIYOYKYBXIYOYKOOXTXNYAYOXTXNUBXI
      YKYMYNXRXKXSXMXJCDUCXLCDUCUDUEXIYOYKXNYAQZXIYOXJHTZXLHTZOZYKYTQXIYMUUAYNU
      UBCHXJUFCHXLUFUGYKXNYEYIUHZMZUUCYAYKUUDXNYKUUDYGXOUHZXOYEYGYIXOUIXOXOXOUH
      UUFXOUJXOYGXOXNYFXKXMUKULUMUNUOUPXJUQZXLUQZUUEYAQUUAUUBUUGUUHOYAUUEXJXLUR
      USXJUTXLUTVAVBVCVDVEVFVGVHVHYKABCCVIYBABCCVIVJXPYJBCNZACNZYLYMYIOZXOQZBUA
      AUAYOYJQZBUAAUAXPUUJUULUUMABUULYOYIXOYOYIOUUKXOYOYMYIYMYNVKVLVMVNVOXOABCX
      JVIYJABCCVIVPUUJYHBCNACNZUUJOYLUUJUUNUUJYHACNZBCNZUUNUUJFJZXJTZXKUUQDKZLZ
      MZQZFCNZACNUUQGJZTZUVDDKZUUSLZMZQZFCNZGCNZUUPUUIUVCACYJUVBBFCXLUUQLZYIUUR
      XOUVAXLUUQXJVQUVLXNUUTUVLXMUUSXKXLUUQDVRVSVTWAWBWCUVCUVJAGCXJUVDLZUVBUVIF
      CUVMUURUVEUVAUVHXJUVDUUQWDUVMUUTUVGUVMXKUVFUUSXJUVDDVRWEVTWAWFWBUVKXJUVDT
      ZUVFXKLZMZQZACNZGCNUUPUVJUVRGCUVIUVQFACUUQXJLZUVEUVNUVHUVPUUQXJUVDVQUVSUV
      GUVOUVSUUSXKUVFUUQXJDVRVSVTWAWBWCUVRUUOGBCUVDXLLZUVQYHACUVTUVNYEUVPYGUVDX
      LXJWDUVTUVOYFUVTUVFXMXKUVDXLDVRWEVTWAWFWBWGWHYHBACWIWJWKYHYJABCCWLWMWNWOW
      PXQCWQZYCYDXICWRXQWSZYCYDUWAUWBYCOZUWBYDUWCCWRXQWTUWBYDOABCWRXQXACWRXQXBU
      NXCCXQXDXEDHWQXIUWAEHCDXFXGXHWN $.
      $( [9-Feb-1997] $)

    $( Proposition 7.48(1) of [TakeutiZaring] p. 51. $)
    tz7.48-1 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) ->
                   ran F C_ A ) $=
      ( vy cv cfv cima cdif wcel con0 wral crn wceq wa wex hbra1 ax-17 wi ra4
      eleq1 eldifi syl5cbi imim2i imp3a syl 19.23ad cop visset elrn2 cdm
      opeldm wfn fndm ax-mp syl6eleq ancri wb fnopfvb mpan pm5.32i sylibr
      eximi sylbi syl5 ssrdv ) AFZCGZBCVGHZIJZAKLZECMZBVKVGKJZVHEFZNZOZAPZVNBJZ
      VNVLJZVKVPVRAVJAKQVRARVKVMVJSZVPVRSVJAKTVTVMVOVRVJVOVRSVMVOVHBJVRVJVHVNBU
      AVHBVIUBUCUDUEUFUGVSVGVNUHCJZAPVQAVNCEUIZUJWAVPAWAVMWAOVPWAVMWAVGCUKZKVGV
      NCAUIULCKUMZWCKNDKCUNUOUPUQVMVOWAWDVMVOWAURDKVGVNCWBUSUTVAVBVCVDVEVF $.
      $( [9-Feb-1997] $)

    $( Proposition 7.48(2) of [TakeutiZaring] p. 51. $)
    tz7.48-2 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) -> Fun `' F ) $=
      ( vy cv cfv cima cdif wcel con0 wral cres ccnv wfun wss wceq wn
      tz7.48lem ssid wa wi onelon ancoms cdm crn cin dmres eleq2i elin bitri
      wfn fnfun ax-mp funres fvelrn mpan sylbir wb fvres eleq1d df-ima
      syl6rbbr adantr mpbird eleq1a eldifn nsyli syl fndm sylan2br syldan
      expimpd com12 r19.21aiv ralimiaa sylancr wrel fnrel eqimssi relssres
      mp2an cnveqi funeq sylib ) AFZCGZBCWFHZIJZAKLZCKMZNZOZCNZOZKKPWGEFZCGZQZR
      ZEWFLZAKLWMWJAEKCDSKTWIWTAKWFKJZWIUAZWSEWFWPWFJZXBWSXCXAWIWSXCXAWPKJZWIWS
      UBZXAXCXDWFWPUCUDXCWPCUEZJZXEXDXCXGUAZWQWHJZXEXHXIWPCWFMZGZXJUFZJZXHWPXJU
      EZJZXMXOWPWFXFUGZJXHXNXPWPCWFUHUIWPWFXFUJUKXJOZXOXMCOZXQCKULZXRDKCUMUNWFC
      UOUNWPXJUPUQURXCXIXMUSXGXCXMWQXLJXIXCXKWQXLWPWFCUTVAWHXLWQCWFVBUIVCVDVEXI
      WRWGWHJWIWQWHWGVFWGBWHVGVHVIXFKWPXSXFKQDKCVJUNZUIVKVLVMVNVOVPVQWLWNQWMWOU
      SWKCCVRZXFKPWKCQXSYADKCVSUNXFKXTVTCKWAWBWCWLWNWDUNWE $.
      $( [9-Feb-1997] $)

    $( Proposition 7.48(3) of [TakeutiZaring] p. 51. $)
    tz7.48-3 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) ->
                    -. A e. _V ) $=
      ( cv cfv cima cdif wcel con0 wral cvv crn cdm onprc wfn wceq fndm ax-mp
      eleq1i mtbir ccnv wfun wi tz7.48-2 funrnex com12 df-rn dfdm4 3imtr4g syl
      mtoi wss tz7.48-1 ssexg ex mtod ) AEZCFBCURGHIAJKZBLIZCMZLIZUSVBCNZLIZVDJ
      LIOVCJLCJPVCJQDJCRSTUAUSCUBZUCZVBVDUDABCDUEVFVENZLIZVEMZLIZVBVDVHVFVJLVEU
      FUGVAVGLCUHTVCVILCUITUJUKULUSVABUMZUTVBUDABCDUNVKUTVBVABLUOUPUKUQ $.
      $( [9-Feb-1997] $)

    ${
      tz7.49.2 $e |- A e. _V $.
      $( Proposition 7.49 of [TakeutiZaring] p. 51. $)
      tz7.49 $p |- ( A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                   ( F ` x ) e. ( A \ ( F " x ) ) ) ->
                   E. x e. On ( A. y e. x ( A \ ( F " y ) ) =/= (/) /\
                   ( F " x ) = A /\ Fun `' ( F |` x ) ) ) $=
        ( vz cv cima cdif c0 wne cfv wcel wi con0 wral wceq wa wrex cres ccnv
        wfun w3a wn cvv tz7.48-3 mt2 ralim df-ne ralbii syl5ibr mtoi dfrex2
        sylibr imaeq2 difeq2d eqeq1d onminex syl anbi2i rexbii hbra1 simpllr
        wss ax-17 hban ra4 adantld onelon neeq1d fveq2 eleq12d imbi12d rcla4v
        com23 sylcom com3r imp exp3a eleq1 eldifi syl5cbi syl8 com34 r19.23ad
        wfn fnfun ax-mp fvelima mpan syl5 ssrdv ssdif0 biimpri anim12i eqss
        wal ssel cdm funfvima2 onss fndm syl6ssr sylancr syl6 com12 a1i syldd
        imp4a eleq1a con3d eldifn syl5com com4r 19.21adv 19.21ad r2al syl6ibr
        ancld tz7.48lem ancoms adantr 3jca exp41 reximdai mpd ) CDAHZIZJZKLZYHD
        MZYJNZOZAPQZYJKRZCDBHZIZJZKLZBYHQZSZAPTZUUAYICRZDYHUAUBUCZUDZAPTYOYPYSK
        RZUEZBYHQZSZAPTZUUCYOYPAPTZUUKYOYPUEZAPQZUEUULYOUUNYMAPQZUUOCUFNFACDEUG
        UHYOYKAPQUUOUUNYKYMAPUIYKUUMAPYJKUJUKULUMYPAPUNUOYPUUGABYHYQRZYJYSKUUPY
        IYRCYHYQDUPUQZURUSUTUUBUUJAPUUAUUIYPYTUUHBYHYSKUJUKVAVBUOYOUUBUUFAPYNAP
        VCYOYHPNZYPUUAUUFYOUURUUAYPUUFYOUUAUURYPUUFOYOUUAUURYPUUFYOUUASZUURSZYP
        SZUUAUUDUUEYOUUAUURYPVDUVAYICVEZCYIVEZSUUDUUTUVBYPUVCUUTGYICUUSUURGHZYI
        NZUVDCNZOUUSUVEUURUVFUUSYQDMZUVDRZBYHTZUURUVFOZUVEUUSUVHUVJBYHYOUUABYOB
        VFZYTBYHVCZVGUVJBVFUUSYQYHNZUURUVHUVFUUSUVMUURUVGYSNZUVHUVFOUUSUURUVMUV
        NUUSUURUVMUVNYOUUAUURUVMSZUVNOUUAUVOYOUVNUUAUVOYTYOUVNOZUUAUVMYTUURYTBY
        HVHZVIUVOYQPNZYTUVPOZYHYQVJUVRYOYTUVNYNYTUVNOAYQPUUPYKYTYMUVNUUPYJYSKUU
        QVKUUPYLUVGYJYSYHYQDVLUUQVMVNVOVPZUTVQVRVSVTVPUVHUVGCNUVFUVNUVGUVDCWAUV
        GCYRWBWCWDWEWFDUCZUVEUVIDPWGZUWAEPDWHWIZBUVDYHDWJWKWLVPVSWMUVCYPCYIWNWO
        WPYICWQUOUUTUUEYPUUSUURUUEUUAYOUURUUEOUUAYOSZYHPVEZUUEUURUWDUWEUWEUVGUV
        DDMZRZUEZGYQQBYHQZSUUEUWDUWEUWIUWDUWEUVMUVDYQNZSUWHOZGWRZBWRUWIUWDUWEUW
        LBUUAYOBUVLUVKVGUWEBVFUWDUWEUWKGUWDUWEUVMUWJUWHUWEUVMUWJUWDUWHUWEUVMUWJ
        UWFYRNZUWDUWHOUWEUVMUVRUWJUWMOZYHPYQWSZUWAYQDWTZVEUWNUVRYQUVDDXAUWCUVRY
        QPUWPYQXBUWBUWPPREPDXCWIXDXEXFUWEUVMUWDUWMUWHUWEUVMUWDUVNUWMUWHOUWEUVMU
        UAYOUVNUWEUVMUUAYTUVPUVMUUAYTOOUWEUUAUVMYTUVQXGXHUWEUVMUVRUVSUWOUVTXFXI
        XJUWMUVGYRNZUEUWHUVNUWMUWGUWQUWFYRUVGXKXLUVGCYRXMXNWDWEXIXOXJXPXQUWHBGY
        HYQXRXSXTBGYHDEYAXFYHXBWLYBVSYCYDYEVPWEXJYFYG $.
        $( [4-May-2007] $) $( [10-Feb-1997] $)

      $( Corollary of Proposition 7.49 of [TakeutiZaring] p. 51. $)
      tz7.49c $p |- ( A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                   ( F ` x ) e. ( A \ ( F " x ) ) ) ->
                   E. x e. On ( F |` x ) : x -1-1-onto-> A ) $=
        ( vy cv cima cdif c0 wne cfv wcel wi con0 wral wceq cres ccnv wfun w3a
        wrex wf1o tz7.49 wa wfn crn wss fnssres onss sylancr df-ima eqeq1i
        biimpi anim12i anim1i dff1o2 df-3an an23 3bitri sylibr expl 3simpc
        syl5 reximia syl ) BCAGZHZIZJKVGCLVIMNAOPBCFGHIJKFVGPZVHBQZCVGRZSTZUAZA
        OUBVGBVLUCZAOUBAFBCDEUDVNVOAOVGOMZVKVMUEVOVNVPVKVMVOVPVKUEZVMUEVLVGUFZV
        LUGZBQZUEZVMUEZVOVQWAVMVPVRVKVTCOUFVGOUHVRVPOVGCUIDVGUJUKVKVTVHVSBCVGUL
        UMUNUOUPVOVRVMVTUAVRVMUEVTUEWBVGBVLUQVRVMVTURVRVMVTUSUTVAVBVJVKVMVCVDVE
        VF $.
        $( [5-May-2007] $) $( [10-Feb-1997] $)
    $}
  $}

