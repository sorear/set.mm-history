$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cantor's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y F $.
    canth.1 $e |- A e. V $.
    $( No set ` A ` is equinumerous to its power set (Cantor's theorem), i.e.
       no function can map ` A ` it onto its power set.  Compare Theorem 6B(b)
       of [Enderton] p. 132.  For the equinumerosity version, see ~ canth2 .
       Note that ` A ` must be a set:  this theorem does not hold when ` A ` is
       too large to be a set; see ~ ncanth for a counterexample.  (Use ~ nex
       if you want the form ` -. E. f f : A -onto-> P~ A ` .) $)
    canth $p |- -. F : A -onto-> P~ A $=
      ( vx vy cpw wfo crn wceq forn wf cv cfv wcel wn crab fof wral wb id
      fveq2 eleq12d negbid elrab baibr nbbn eleq2 con3i sylbi syl rgen wrex
      wfn wi ffn fvelrnb biimpd con3d ralnex syl5ib mpi wss ssrab2 rabex elpw
      mpbir mpbiri 3syl pm2.65i ) AAFZBGZBHZVJIZAVJBJVKAVJBKZDLZVOBMZNZOZDAPZVL
      NZOZVMOAVJBQVNELZBMZVSIZOZEARZWAWEEAWBANZWBWCNZOZWBVSNZSZWEWJWGWIVRWIDWBA
      VOWBIZVQWHWLVOWBVPWCWLTVOWBBUAUBUCUDUEWKWHWJSZOWEWHWJUFWDWMWCVSWBUGUHUIUJ
      UKVNWDEAULZOWAWFVNVTWNVNBAUMZVTWNUNAVJBUOWOVTWNEAVSBUPUQUJURWDEAUSUTVAVMV
      TVMVTVSVJNZWPVSAVBVRDAVCVSAVRDACVDVEVFVLVJVSUGVGUHVHVI $.
      $( [7-Aug-1994] $)
  $}

  $( Cantor's theorem fails for the universal class (which is not a set but a
     proper class by ~ nvelv ).  Specifically, the identity function maps the
     universe onto its power class.  Compare ~ canth that works for sets.  See
     also the remark in ~ ru about NF, in which Cantor's theorem fails for sets
     that are "too large."  This theorem gives some intuition behind that
     failure:  in NF the universal class is a set, and it equals its own power
     set. $)
  ncanth $p |- I : V -onto-> P~ V $=
    ( cvv cpw cid wf1o wfo f1ovi wceq wb pwv f1oeq3 ax-mp mpbir f1ofo ) AABZCDZ
    ANCEOAACDZFNAGOPHINAACJKLANCMK $.
    $( [29-Jun-2004] $) $( [29-Jun-2004] $)

