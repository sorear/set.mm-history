$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  Functions on ordinals; strictly monotone ordinal functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d y z B $.
    iunon.1 $e |- A e. _V $.
    iunon.2 $e |- B e. _V $.
    $( The indexed union of a set of ordinal numbers ` B ( x ) ` is an ordinal
       number. $)
    iunon $p |- ( A. x e. A B e. On -> U_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral ciun cv wceq wrex cab cuni dfiun2 wss wi ax-17 wal
      abid hbra1 ra4 eleq1a syl6 r19.23ad syl5bi 19.21aiv dfss2f sylibr abrexex
      hbab1 ssonunii syl syl5eqel ) CHIZABJZABCKFLZCMZABNZFOZPZHAFBCEQURVBHRZVC
      HIURUSVBIZUSHIZSZFUAVDURVGFVEVAURVFVAFUBURUTVFABUQABUCVFATURALBIUQUTVFSUQ
      ABUDCHUSUEUFUGUHUIFGVBHVAFGUMGLHIFTUJUKVBAFBCDULUNUOUP $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    iinon.1 $e |- B e. _V $.
    $( The nonempty indexed intersection of a class of ordinal numbers
       ` B ( x ) ` is an ordinal number. $)
    iinon $p |- ( ( A. x e. A B e. On /\ A =/= (/) ) ->
                |^|_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral c0 wne wa ciin cv wceq wrex wi ax-17 wex exbii cab
      cint dfiin2 wss wal abid hbra1 eleq1a syl6 r19.23ad syl5bi 19.21aiv hbab1
      ra4 dfss2f sylibr df-rex excom 19.42v isseti mpbiran2 3bitr2ri n0 3bitr4i
      abn0 oninton sylan2b sylan syl5eqel ) CGHZABIZBJKZLABCMENZCOZABPZEUAZUBZG
      AEBCDUCVKVPGUDZVLVQGHZVKVMVPHZVMGHZQZEUEVRVKWBEVTVOVKWAVOEUFVKVNWAABVJABU
      GWAARVKANBHZVJVNWAQVJABUNCGVMUHUIUJUKULEFVPGVOEFUMFNGHERUOUPVLVRVPJKZVSWC
      ASZVOESZVLWDWFWCVNLZASZESWGESZASWEVOWHEVNABUQTWGAEURWIWCAWIWCVNESWCVNEUSE
      CDUTVATVBABVCVOEVEVDVPVFVGVHVI $.
      $( [17-May-2007] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x y S $.  $d x y F $.  $d x T $.
    onfununi.1 $e |- ( Lim y -> ( F ` y ) = U_ x e. y ( F ` x ) ) $.
    onfununi.2 $e |- ( ( x e. On /\ y e. On /\ x C_ y )
                  -> ( F ` x ) C_ ( F ` y ) ) $.
    $( A property of functions on ordinal numbers.  Generalization of Theorem
       Schema 8E of [Enderton] p. 218.  (Contributed by Eric Schmidt,
       26-May-2009.) $)
    onfununi $p |- ( ( S e. T /\ S C_ On /\ S =/= (/) )
                -> ( F ` U. S ) = U_ x e. S ( F ` x ) ) $=
      ( wcel con0 wss w3a cfv ciun wa wceq word wi syl6 imp adantr c0 wne cv wn
      cuni wlim ssorduni ad2antrr nelneq wo elssuni adantl ssel eloni ordsseleq
      syl2an anabss1 mpbid ord con1d syl5 exp4b pm2.43d com23 ssrdv sylan uniss
      wb ssn0 syl orduniss eqssd 3jca df-lim an32s 3adantl1 ssonuni limeq fveq2
      sylibr iuneq1 eqeq12d imbi12d vtoclg 3adant3 mpd wral wrex eluni2 adantrd
      anim1d onelon ordelss syland 3jcad exp3a reximdvai syl5bi ssiun r19.21aiv
      a1i iunss cbviunv syl6sseq 3ad2ant2 eqsstrd ssiun2s pm2.61d2 sseq2 anbi2d
      ex jcad sseq2d 3com12 3expib vtoclga sylsyld ) CDHZCIJZCUAUBZKZCUEZELZACA
      UCZELZMZYAYBCHZYCYFJZYAYGUDZYHYAYINZYCAYBYEMZYFYJYBUFZYCYKOZXSXTYIYLXRXSY
      IXTYLXSYINZXTNZYBPZYBUAUBZYBYBUEZOZKYLYOYPYQYSXSYPYIXTCUGZUHYNCYBJZXTYQYN
      ACYBXSYIYDCHZYDYBHZQXSUUBYIUUCXSUUBYIUUCQXSUUBUUBYIUUCUUBYINYDYBOZUDXSUUB
      NZUUCYDYBCUIUUEUUCUUDUUEUUCUUDUUEYDYBJZUUCUUDUJZUUBUUFXSYDCUKZULXSUUBUUFU
      UGVHZUUEYDPZYPUUIXSXSUUBUUJXSUUBYDIHZUUJCIYDUMZYDUNRSYTYDYBUOUPUQURUSUTVA
      VBVCVDSVEZCYBVIVFYNYSXTYNYBYRYNUUAYBYRJUUMCYBVGVJXSYRYBJZYIXSYPUUNYTYBVKV
      JTVLTVMYBVNVTVOVPYAYLYMQZYIXRXSUUOXTXRXSUUOXRXSYBIHZUUOCDVQZBUCZUFZUURELZ
      AUURYEMZOZQUUOBYBIUURYBOZUUSYLUVBYMUURYBVRUVCUUTYCUVAYKUURYBEVSZAUURYBYEW
      AWBWCFWDRSWETWFYAYKYFJZYIXSXRUVEXTXSYKBCUUTMZYFXSYEUVFJZAYBWGYKUVFJXSUVGA
      YBXSUUCYEUUTJZBCWHZUVGUUCYDUURHZBCWHXSUVIBYDCWIXSUVJUVHBCXSUURCHZUVJUVHXS
      UVKUVJNZUUKUURIHZYDUURJZKUVHXSUVLUUKUVMUVNXSUVLUVMUVJNUUKXSUVKUVMUVJCIUUR
      UMZWKUURYDWLRXSUVKUVMUVJUVOWJXSUVKUURPZUVJUVNXSUVKUVMUVPUVOUURUNRUVPUVJNU
      VNQXSUURYDWMXAWNWOGRWPWQWRBCUUTYEWSRWTAYBYEUVFXBVTBACUUTYEUURYDEVSXCXDXET
      XFXKACYEYBYCYDYBEVSXGXHYAYEYCJZACWGYFYCJYAUVQACYAUUPUUBUUKUUFNZUVQXRXSUUP
      XTXRXSUUPUUQSWEYAUUBUUKUUFXSXRUUBUUKQXTUULXEUUBUUFQYAUUHXAXLUUKUVNNZUVHQU
      VRUVQQBYBIUVCUVSUVRUVHUVQUVCUVNUUFUUKUURYBYDXIXJUVCUUTYCYEUVDXMWCUVMUUKUV
      NUVHUUKUVMUVNUVHGXNXOXPXQWTACYEYCXBVTVL $.
      $( [26-May-2009] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.  $d w x y z K $.  $d w x y L $.
    $d w x y z S $.
    onopruni.1 $e |- ( Lim y -> ( A F y ) = U_ x e. y ( A F x ) ) $.
    onopruni.2 $e |- ( ( x e. On /\ y e. On /\ x C_ y )
                  -> ( A F x ) C_ ( A F y ) ) $.
    ${
      $d x T $.
      $( A variant of ~ onfununi for operations.  (Contributed by Eric Schmidt,
         26-May-2009.) $)
      onopruni $p |- ( ( S e. T /\ S C_ On /\ S =/= (/) )
                     -> ( A F U. S ) = U_ x e. S ( A F x ) ) $=
        ( vw vz wcel con0 cv co wceq cfv ciun oprex opreq2 cvv wss wne w3a cuni
        c0 copab wlim visset fvopab a1i iuneq2i 3eqtr4g 3sstr4g onfununi uniexg
        fvopabg sylancl 3ad2ant1 3eqtr3d ) DEKZDLUAZDUEUBZUCZDUDZIMCJMZFNZOJIUF
        ZPZADAMZVGPZQZCVDFNZADCVIFNZQZABDEVGBMZUGCVOFNZAVOVMQVOVGPZAVOVJQGJIVOV
        FVPBUHCVOFRVEVOCFSUIZAVOVJVMVJVMOZVIVOKJIVIVFVMAUHCVIFRVEVICFSUIZUJUKUL
        VILKVOLKVIVOUAUCVMVPVJVQHVTVRUMUNUTVAVHVLOZVBUTVDTKVLTKWADEUOCVDFRJIVDV
        FVLTTVEVDCFSUPUQURVKVNOVCADVJVMVSVIDKVTUJUKUJUS $.
        $( [26-May-2009] $)
    $}

    $( A variant of ~ onopruni with indexed unions.  (Contributed by Eric
       Schmidt, 26-May-2009.)  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 19-Jun-2012.) $)
    onopriun $p |- ( ( K e. T /\ A. z e. K L e. On /\ K =/= (/) )
                -> ( A F U_ z e. K L ) = U_ z e. K ( A F L ) ) $=
      ( vw wcel con0 ciun co cv wceq wrex wa wex c0 wne w3a cab dfiun2g opreq2d
      wral cuni 3ad2ant2 cvv wss abrexexg 3ad2ant1 hbra1 ax-17 ra4 biimprd syl9
      eleq1 com23 r19.23ad abssdv r19.2z reximi syl rexcom4 sylib sylibr ancoms
      elex abn0 3adant1 onopruni syl111anc visset rexbidv anbi1i r19.41v bitr4i
      eqeq1 elab exbii df-rex 3bitr4i imp opreq2 eqeq2d ceqsexgv rexbida syl5bb
      wb abbidv unieqd oprex dfiun2 3eqtr4g 3eqtrd ) GELZHMLZCGUGZGUAUBZUCZDCGH
      NZFOZDKPZHQZCGRZKUDZUHZFOZAXHDAPZFOZNZCGDHFOZNZWTWRXDXJQXAWTXCXIDFCKGHMUE
      UFUIXBXHUJLZXHMUKZXHUAUBZXJXMQWRWTXPXACKGHEULUMWTWRXQXAWTXGKMWTXFXEMLZCGW
      SCGUNZXSCUOWTXFCPGLZXSWTYAWSXFXSWSCGUPZXFXSWSXEHMUSUQURUTVAVBUIWTXAXRWRXA
      WTXRXAWTSZXGKTZXRYCXFKTZCGRZYDYCWSCGRYFWSCGVCWSYECGKHMVJVDVEXFCKGVFVGXGKV
      KVHVIVLABDXHUJFIJVMVNWTWRXMXOQXAWTBPZXLQZAXHRZBUDZUHYGXNQZCGRZBUDZUHXMXOW
      TYJYMWTYIYLBYIXKHQZYHSZATZCGRZWTYLXKXHLZYHSZATYOCGRZATYIYQYSYTAYSYNCGRZYH
      SYTYRUUAYHXGUUAKXKAVOXEXKQXFYNCGXEXKHVTVPWAVQYNYHCGVRVSWBYHAXHWCYOCAGVFWD
      WTYPYKCGXTWTYASWSYPYKWKWTYAWSYBWEYHYKAHMYNXLXNYGXKHDFWFWGWHVEWIWJWLWMABXH
      XLDXKFWNWOCBGXNDHFWNWOWPUIWQ $.
      $( [24-Jul-2012] $) $( [26-May-2009] $)
  $}

  $c Smo $.

  $( Introduce the strictly monotone ordinal function.  A strictly monotone
     function is one that is constantly increasing across the ordinals. $)
  wsmo $a wff Smo A $.

  ${
    $d x y A $.
    $( Definition of a strictly monotone ordinal function.  Definition 7.46 in
       [TakeutiZaring] p. 50. $)
    df-smo $a |- ( Smo A <-> ( A : dom A --> On /\ Ord dom A /\ A. x e. dom A
       A. y e. dom A ( x e. y -> ( A ` x ) e. ( A ` y ) ) ) ) $.
  $}

  ${
    $d F x y $.
    $( Alternate definition of a strictly monotone ordinal function.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    dfsmo2 $p |- ( Smo F <-> ( F : dom F --> On /\ Ord dom F /\ A. x e. dom F
       A. y e. x ( F ` y ) e. ( F ` x ) ) ) $=
      ( wsmo cdm con0 wf word wel cfv wcel wral w3a df-smo ralcom impexp 3anass
      cv wi wa simpr ordtr1 3impib 3com23 simp3 3expia impbid2 syl5bbr ralbidv2
      jca imbi1d ralbidva syl5bb pm5.32i anbi2i 3bitr4i bitri ) CDCEZFCGZURHZBA
      IZBRZCJARZCJKZSZAURLBURLZMZUSUTVDBVCLZAURLZMZBACNUSUTVFTZTUSUTVITZTVGVJVK
      VLUSUTVFVIVFVEBURLZAURLUTVIVEBAURUROUTVMVHAURUTVCURKZTZVEVDBURVCVBURKZVES
      VPVATZVDSVOVEVPVAVDPVOVQVAVDVOVQVAVPVAUAUTVNVAVQUTVNVAMVPVAUTVAVNVPUTVAVN
      VPVBVCURUBUCUDUTVNVAUEUJUFUGUKUHUIULUMUNUOUSUTVFQUSUTVIQUPUQ $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d x y A $.
    issmo.1 $e |- A : B --> On $.
    issmo.2 $e |- Ord B $.
    issmo.3 $e |- ( ( x e. B /\ y e. B ) -> ( x e. y -> ( A ` x ) e. ( A ` y
       ) ) ) $.
    issmo.4 $e |- dom A = B $.
    $( Conditions for which ` A ` is a strictly monotone ordinal function.
       (Contributed by Andrew Salmon, 15-Nov-2011.) $)
    issmo $p |- Smo A $=
      ( wsmo cdm con0 wf word cv wcel cfv wi wral mpbir eleq2i feq2i wceq ordeq
      df-smo wb ax-mp syl2anb rgen2a mpbir3an ) CICJZKCLZUJMZANZBNZOUMCPUNCPOQZ
      BUJRAUJRABCUDUKDKCLEUJDKCHUASULDMZFUJDUBULUPUEHUJDUCUFSUOABUJUMUJOUMDOUND
      OUOUNUJOUJDUMHTUJDUNHTGUGUHUI $.
      $( [15-Nov-2011] $)
  $}

  ${
    $d A x $.  $d F x y $.
    $( Alternative definition of a strictly monotone ordinal function.
       (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    issmo2 $p |- ( F : A --> B -> ( ( B C_ On /\ Ord A /\
       A. x e. A A. y e. x ( F ` y ) e. ( F ` x ) ) -> Smo F ) ) $=
      ( wf con0 wss word cv cfv wcel wral w3a cdm wsmo fss ex fdm biimprd feq2d
      sylibrd wceq wb ordeq syl raleqdv 3anim123d dfsmo2 syl6ibr ) CDEFZDGHZCIZ
      BJEKAJZEKLBUNMZACMZNEOZGEFZUQIZUOAUQMZNEPUKULURUMUSUPUTUKULCGEFZURUKULVAC
      DGEQRUKUQCGECDESZUAUBUKUSUMUKUQCUCUSUMUDVBUQCUEUFTUKUTUPUKUOAUQCVBUGTUHAB
      EUIUJ $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for strictly monotone functions.  (Contributed by
       Andrew Salmon, 16-Nov-2011.) $)
    smoeq $p |- ( A = B -> ( Smo A <-> Smo B ) ) $=
      ( vx vy wceq cdm con0 wf word cv wcel cfv wi wral w3a wsmo wb fveq1 raleq
      syl id dmeq feq12d ordeq eleq12d imbi2d 2ralbidv ralbidv 3bitrd 3anbi123d
      df-smo 3bitr4g ) ABEZAFZGAHZUNIZCJZDJZKZUQALZURALZKZMZDUNNCUNNZOBFZGBHZVE
      IZUSUQBLZURBLZKZMZDVENZCVENZOAPBPUMUOVFUPVGVDVMUMUNVEGABUMUAABUBZUCUMUNVE
      EZUPVGQVNUNVEUDTUMVDVKDUNNZCUNNVLCUNNZVMUMVCVKCDUNUNUMVBVJUSUMUTVHVAVIUQA
      BRURABRUEUFUGUMVPVLCUNUMVOVPVLQVNVKDUNVESTUHUMVOVQVMQVNVLCUNVESTUIUJCDAUK
      CDBUKUL $.
      $( [16-Nov-2011] $)

    $( The domain of a strictly monotone function is an ordinal.  (Contributed
       by Andrew Salmon, 16-Nov-2011.) $)
    smodm $p |- ( Smo A -> Ord dom A ) $=
      ( vx vy wsmo cdm con0 wf word cv wcel cfv wi wral w3a df-smo simp2 sylbi
      ) ADAEZFAGZRHZBIZCIZJUAAKUBAKJLCRMBRMZNTBCAOSTUCPQ $.
      $( [16-Nov-2011] $)

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone.  (Contributed by Andrew Salmon, 16-Nov-2011.) $)
    smores $p |- ( ( Smo A /\ B e. dom A ) -> Smo ( A |` B ) ) $=
      ( vx vy cdm wcel wsmo con0 wf word cv cfv wi wral w3a wfn crn wss wa wceq
      cres wfun funres funfn 3imtr3i resss rnss ax-mp sstr mpan anim12i 3imtr4i
      df-f a1i ordelord expcom ordin ex syli wb dmres ordeq syl6ibr hbra1 hbra2
      cin ax-17 eleq2i anbi12i elin pm2.27 fvres adantr adantl eleq12d biimprcd
      ra42 imim2i com23 syl6 impcom syl5 an4s com12 r19.21ad r19.21ai 3anim123d
      syl5bi exp3a df-smo 3imtr4g ) BAEZFZAGZABUAZGZWMWLHAIZWLJZCKZDKZFZWSALZWT
      ALZFZMZDWLNZCWLNZOWOEZHWOIZXHJZXAWSWOLZWTWOLZFZMZDXHNZCXHNZOWNWPWMWQXIWRX
      JXGXPWQXIMWMAWLPZAQZHRZSWOXHPZWOQZHRZSWQXIXQXTXSYBAUBWOUBXQXTBAUCAUDWOUDU
      EYAXRRZXSYBWOARYCABUFWOAUGUHYAXRHUIUJUKWLHAUMXHHWOUMULUNWMWRBWLVFZJZXJWRW
      MBJZYEWRWMYFWLBUOUPYFWRYEBWLUQURUSXHYDTXJYEUTABVAZXHYDVBUHVCXGXPMWMXGXOCX
      HXFCWLVDXGWSXHFZXNDXHXECDWLWLVEYHDVGXGYHWTXHFZXNYHYISWSYDFZWTYDFZSZXGXNYH
      YJYIYKXHYDWSYGVHXHYDWTYGVHVIYLWSBFZWSWLFZSZWTBFZWTWLFZSZSZXGXNYJYOYKYRWSB
      WLVJWTBWLVJVIYSXGXNYMYPYNYQXGXNMXGYNYQSZXEMZYMYPSZYTSXNXECDWLWLVQYTUUBUUA
      XNMYTUUAUUBXNYTUUAXEUUBXNMYTXEVKXEXAUUBXMXDUUBXMMXAUUBXMXDUUBXKXBXLXCYMXK
      XBTYPWSBAVLVMYPXLXCTYMWTBAVLVNVOVPVRVSVTVSWAWBWCWDWHWHWIWEWFUNWGCDAWJCDWO
      WJWKWA $.
      $( [16-Nov-2011] $)

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone.  (Contributed by Andrew Salmon, 19-Nov-2011.) $)
    smores3 $p |- ( ( Smo ( A |` B ) /\ C e. ( dom A i^i B ) /\ Ord B ) -> Smo
       ( A |` C ) ) $=
      ( cres wsmo cdm cin wcel word w3a dmres incom eqtri eleq2i smores 3adant3
      sylan2br wss wceq wb inss2 sseli ordelss sylan 3adant1 resabs1 smoeq 3syl
      ancoms mpbid ) ABDZEZCAFZBGZHZBIZJZUKCDZEZACDZEZULUOUSUPUOULCUKFZHUSVBUNC
      VBBUMGUNABKBUMLMNUKCOQPUQCBRZURUTSUSVATUOUPVCULUOCBHZUPVCUNBCUMBUAUBUPVDV
      CBCUCUIUDUEACBUFURUTUGUHUJ $.
      $( [19-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    $( A strictly monotone ordinal function restricted to an ordinal is still
       monotone. $)
    smores2 $p |- ( ( Smo F /\ Ord A ) -> Smo ( F |` A ) ) $=
      ( vy vx wsmo word wa cdm con0 wf cv cfv wcel wral crn wss wfun dfsmo2 syl
      wceq cres wfn simp1bi ffun funres funfn sylib frn df-ima imassrn eqsstr3i
      3syl cima sstr mpan df-f sylanbrc adantr smodm ordin wb dmres ordeq ax-mp
      cin sylibr ancoms sylan resss dmss simp3bi ssralv mpsyl wi ordtr1 eqsstri
      wel inss1 sseli syl6 exp3acom23 imp31 fvres ad2antlr eleq12d ralbidva w3a
      mpbird biimpri syl111anc ) BEZAFZGZBAUAZHZIWNJZWOFZCKZWNLZDKZWNLZMZCWTNZD
      WONZWNEZWKWPWLWKWNWOUBZWNOZIPZWPWKBHZIBJZBQZXFWKXJXIFZWRBLZWTBLZMZCWTNZDX
      INZDCBRZUCZXIIBUDXKWNQXFABUEWNUFUGULWKXJBOZIPZXHXSXIIBUHXGXTPYAXHXGBAUMXT
      BAUIBAUJUKXGXTIUNUOULWOIWNUPUQURWKXLWLWQBUSWLXLWQWLXLGAXIVEZFZWQAXIUTWOYB
      TWQYCVABAVBZWOYBVCVDVFVGVHZWMXDXPDWONZWKYFWLWOXIPZWKXQYFWNBPYGBAVIWNBVJVD
      WKXJXLXQXRVKXPDWOXIVLVMURWMXCXPDWOWMWTWOMZGZXBXOCWTYICDVQZGZWSXMXAXNYKWRA
      MZWSXMTWMYHYJYLWMYJYHYLWMYJYHGZWRWOMZYLWMWQYMYNVNYEWRWTWOVOSWOAWRWOYBAYDA
      XIVRVPZVSVTWAWBWRABWCSYHXAXNTZWMYJYHWTAMYPWOAWTYOVSWTABWCSWDWEWFWFWHXEWPW
      QXDWGDCWNRWIWJ $.
      $( [15-Mar-2013] $)
  $}

  $( The domain of a strictly monotone ordinal function is an ordinal.
     (Contributed by Mario Carneiro, 12-Mar-2013.) $)
  smodm2 $p |- ( ( F Fn A /\ Smo F ) -> Ord A ) $=
    ( wsmo wfn cdm word smodm wceq wb fndm ordeq syl biimpa sylan2 ) BCBADZBEZF
    ZAFZBGOQROPAHQRIABJPAKLMN $.
    $( [12-Mar-2013] $)

  ${
    $d F x y $.
    $( The function values of a strictly monotone ordinal function are
       ordinals.  (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    smofvon2 $p |- ( Smo F -> ( F ` B ) e. On ) $=
      ( vy vx cdm wcel wsmo cfv con0 wi word wral dfsmo2 simp1bi ffvelrn expcom
      wf cv syl5 wn c0 ndmfv 0elon syl6eqel a1d pm2.61i ) ABEZFZBGZABHZIFZJUIUG
      IBQZUHUKUIULUGKCRBHDRZBHFCUMLDUGLDCBMNULUHUKUGIABOPSUHTZUKUIUNUJUAIABUBUC
      UDUEUF $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d x y A $.
    iordsmo.1 $e |- Ord A $.
    $( The identity relation restricted to the ordinals is a strictly monotone
       function.  (Contributed by Andrew Salmon, 16-Nov-2011.) $)
    iordsmo $p |- Smo ( _I |` A ) $=
      ( vx vy cid cres con0 wf wfn crn wss df-f fnresi rnresi word cv wcel wceq
      cfv fvresi ordsson ax-mp eqsstri mpbir2an wa adantr adantl eleq12d dmresi
      biimprd issmo ) CDEAFZAAGULHULAIULJZGKAGULLAMUMAGANAOAGKBAUAUBUCUDBCPZAQZ
      DPZAQZUEZUNULSZUPULSZQUNUPQURUSUNUTUPUOUSUNRUQAUNTUFUQUTUPRUOAUPTUGUHUJAU
      IUK $.
      $( [16-Nov-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y F $.
    $( The null set is a strictly monotone ordinal function.  (Contributed by
       Andrew Salmon, 20-Nov-2011.) $)
    smo0 $p |- Smo (/) $=
      ( cid c0 cres wsmo ord0 iordsmo wceq wb res0 smoeq ax-mp mpbi ) ABCZDZBDZ
      BEFMBGNOHAIMBJKL $.
      $( [20-Nov-2011] $)

    $( If ` B ` is a strictly monotone ordinal function, and ` A ` is in the
       domain of ` B ` , then the value of the function at ` A ` is an
       ordinal.  (Contributed by Andrew Salmon, 20-Nov-2011.) $)
    smofvon $p |- ( ( Smo B /\ A e. dom B ) -> ( B ` A ) e. On ) $=
      ( vx vy wsmo cdm con0 wf wcel cfv word cv wi wral w3a simp1 sylbi ffvelrn
      df-smo sylan ) BEZBFZGBHZAUBIABJGIUAUCUBKZCLZDLZIUEBJUFBJIMDUBNCUBNZOUCCD
      BSUCUDUGPQUBGABRT $.
      $( [20-Nov-2011] $)

    $( If ` x ` is less than ` y ` then a strictly monotone function's value
       will be strictly less at ` x ` than at ` y ` .  (Contributed by Andrew
       Salmon, 22-Nov-2011.) $)
    smoel $p |- ( ( Smo B /\ A e. dom B /\ C e. A ) -> ( B ` C ) e. ( B ` A )
       ) $=
      ( vx vy wsmo cdm wcel cfv wa wi word smodm ordtr1 expdimp wral wceq fveq2
      cv imbi12d ancomsd sylan con0 wf df-smo eleq1 eleq1d eleq2 eleq2d rcla42v
      w3a ancoms com12 3ad2ant3 sylbi syld pm2.43d 3impia ) BFZABGZHZCAHZCBIZAB
      IZHZUSVAJZVBVEVFVBCUTHZVBVEKZUSUTLZVAVBVGKBMVIVAVBVGVIVBVAVGCAUTNUAOUBUSV
      AVGVHUSUTUCBUDZVIDSZESZHZVKBIZVLBIZHZKZEUTPDUTPZUKVAVGJZVHKZDEBUEVRVJVTVI
      VSVRVHVGVAVRVHKVQVHCVLHZVCVOHZKDECAUTUTVKCQZVMWAVPWBVKCVLUFWCVNVCVOVKCBRU
      GTVLAQZWAVBWBVEVLACUHWDVOVDVCVLABRUITUJULUMUNUOOUPUQUR $.
      $( [22-Nov-2011] $)

    $( The value of a strictly monotone ordinal function contains its indexed
       union.  (Contributed by Andrew Salmon, 22-Nov-2011.) $)
    smoiun $p |- ( ( Smo B /\ A e. dom B ) -> U_ x e. A ( B ` x )
       C_ ( B ` A ) ) $=
      ( vy wsmo cdm wcel wa cv cfv ciun wrex eliun con0 wi smofvon smoel 3expia
      ontr1 ancomsd exp3a sylsyld r19.23adv syl5bi ssrdv ) CEZBCFGZHZDABAIZCJZK
      ZBCJZDIZUKGUMUJGZABLUHUMULGZAUMBUJMUHUNUOABUHULNGZUIBGZUJULGZUNUOOBCPUFUG
      UQURBCUIQRUPURUNUOUPUNURUOUMUJULSTUAUBUCUDUE $.
      $( [22-Nov-2011] $)

    $( If ` F ` is an isomorphism from an ordinal ` A ` onto ` B ` , which is a
       subset of the ordinals, then ` F ` is a strictly monotonic function.
       Exercise 3 in [TakeutiZaring] p. 50.  (Contributed by Andrew Salmon,
       24-Nov-2011.) $)
    smoiso $p |- ( ( F Isom _E , _E ( A , B ) /\ Ord A /\ B C_ On ) -> Smo F )
        $=
      ( vx vy cep word con0 wss w3a wf cv wcel cfv wral syl wb 3syl wa eleq2d
      wiso cdm wsmo wf1o isof1o f1of ffdm simpld fss sylan 3adant2 syl3an1 wceq
      wi fdm eqcomd ordeq biimpa 3adant3 anbi12d isorel epel fvex epelc 3bitr3g
      wbr biimpd ex sylbid r19.21aivv 3ad2ant1 3jca df-smo sylibr ) ABFFCUAZAGZ
      BHIZJZCUBZHCKZVSGZDLZELZMZWBCNZWCCNZMZUNZEVSODVSOZJCUCVRVTWAWIVOABCKZVPVQ
      VTVOABCUDZWJABFFCUEZABCUFZPWJVQVTVPWJVSBCKZVQVTWJWNVSAIABCUGUHVSBHCUIUJUK
      ULVOVPWAVQVOVPWAVOAVSUMZVPWAQVOWKWJWOWLWMWJVSAABCUOZUPRAVSUQPURUSVOVPWIVQ
      VOWHDEVSVSVOWBVSMZWCVSMZSZWBAMZWCAMZSZWHVOWKWJWSXBQWLWMWJWQWTWRXAWJVSAWBW
      PTWJVSAWCWPTUTRVOXBWHVOXBSZWDWGXCWBWCFVFWEWFFVFWDWGABWBWCFFCVADEVBWEWFWBC
      VCWCCVCVDVEVGVHVIVJVKVLDECVMVN $.
      $( [24-Nov-2011] $)
  $}

  ${
    $d A w x z $.  $d B w z $.  $d F w x y z $.
    $( A strictly monotone ordinal function preserves the epsilon relation.
       (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    smoel2 $p |- ( ( ( F Fn A /\ Smo F ) /\ ( B e. A /\ C e. B ) ) ->
      ( F ` C ) e. ( F ` B ) ) $=
      ( wfn wsmo wa wcel cfv cdm fndm eleq2d anbi1d biimprd smoel 3expib sylan9
      imp ) DAEZDFZGBAHZCBHZGZCDIBDIHZSUCBDJZHZUBGZTUDSUGUCSUFUAUBSUEABADKLMNTU
      FUBUDBDCOPQR $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d A w x y z $.  $d F w x y z $.
    $( A strictly monotone ordinal function is one-to-one.  (Contributed by
       Mario Carneiro, 28-Feb-2013.) $)
    smo11 $p |- ( ( F : A --> B /\ Smo F ) -> F : A -1-1-> B ) $=
      ( vz vw vy vx wa cv cfv weq wi wral wcel word ex w3a 3ad2ant1 fveq2 sylan
      wsmo wceq wf1 simpl wfn ffn smodm2 ordelord syl anim12d wel w3o ordtri3or
      wf simp1rr smoel2 r19.21aivva adantr simp2 simp3 eleq2d raleqbi1dv rcla4v
      eleq1d rcla4cv syl6 3imp eleq1 biimpac syl31anc con0 smofvon2 ordirr 3syl
      wn eloni ad2antlr pm2.65i pm2.21i 3exp ax-1 simp1rl eleq2 3jaod syl5 mpdd
      a1i r19.21aivv dff13 sylanbrc ) ABCUNZCUAZHWKDIZCJZEIZCJZUBZDEKZLZEAMDAMZ
      ABCUCWKWLUDWKCAUEZWLWTABCUFXAWLHZWSDEAAXBWMANZWOANZHZWMOZWOOZHZWSXBXCXFXD
      XGXBAOZXCXFLACUGZXIXCXFAWMUHPUIXBXIXDXGLXJXIXDXGAWOUHPUIUJXBXEXHWSLXHDEUK
      ZWREDUKZULXBXEHZWSWMWOUMXMXKWSWRXLXMXKWQWRXMXKWQQZWRXNWPWPNZXNXDFIZCJZGIZ
      CJZNZFXRMZGAMZXKWQXOXCXDXBXKWQUOXMXKYBWQXBYBXEXBXTGFAXRAXRXPCUPUQURZRXMXK
      WQUSXMXKWQUTXDYBXKQWNWPNZWQXOXDYBXKYDXDYBXQWPNZFWOMZXKYDLYAYFGWOAXTYEFXRW
      OGEKXSWPXQXRWOCSVAVBVCYEYDFWMWOFDKXQWNWPXPWMCSVDVEVFVGWQYDXOWNWPWPVHVITVJ
      XMXKXOVOZWQWLYGXAXEWLWPVKNWPOYGWOCVLWPVPWPVMVNVQZRVRVSVTWRWSLXMWRWQWAWGXM
      XLWQWRXMXLWQQZWRYIXOYIXCYBXLWQXOXCXDXBXLWQWBXMXLYBWQYCRXMXLWQUSXMXLWQUTXC
      YBXLQWPWNNZWQXOXCYBXLYJXCYBXQWNNZFWMMZXLYJLYAYLGWMAXTYKFXRWMGDKXSWNXQXRWM
      CSVAVBVCYKYJFWOWMFEKXQWPWNXPWOCSVDVEVFVGWQYJXOWNWPWPWCVITVJXMXLYGWQYHRVRV
      SVTWDWEPWFWHTDEABCWIWJ $.
      $( [28-Feb-2013] $)
  $}

  ${
    $( A strictly monotone ordinal function preserves strict ordering.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    smoord $p |- ( ( ( F Fn A /\ Smo F ) /\ ( C e. A /\ D e. A ) ) ->
      ( C e. D <-> ( F ` C ) e. ( F ` D ) ) ) $=
      ( wa wcel word cfv ordelord syl11anc w3a simp3 wi smoel2 3expia wn ordirr
      2thd syl 3adant3 wfn wsmo smodm2 adantr simprl simprr wceq ordtri3or expr
      w3o adantrl 3impia eleq2d mtbid con0 smofvon2 ad2antlr eloni 3syl con4bid
      wb fveq2d ordn2lp pm3.2 3ad2ant3 mtod adantrlr 3impb pm3.21 3jaod mp2and
      syl5 ) DAUAZDUBZEZBAFZCAFZEZEZBGZCGZBCFZBDHZCDHZFZVAZVSAGZVPVTVOWGVRADUCU
      DZVOVPVQUEABIJZVSWGVQWAWHVOVPVQUFACIJZVTWAEWBBCUGZCBFZUJVSWFBCUHVSWBWFWKW
      LVOVRWBWFVOVRWBKWBWEVOVRWBLVOVRWBWEVOVQWBWEMVPVOVQWBWEACBDNUIUKULROVOVRWK
      WFVOVRWKKZWBWEWMWBPZWEPZWMBBFZWBVOVRWPPZWKVSVTWQWIBQSTWMBCBVOVRWKLZUMUNWM
      WCWCFZWEVOVRWSPZWKVSWCUOFZWCGZWTVNXAVMVRBDUPUQZWCURZWCQUSTWMWCWDWCWMBCDWR
      VBUMUNRUTOVOVRWLWFVOVRWLKZWBWEXEWNWOXEWBWLWBEZXEWAXFPVOVRWAWLWJTCBVCSWLVO
      WBXFMVRWLWBVDVEVFXEWEWEWDWCFZEZXEXBXHPVOVRXBWLVSXAXBXCXDSTWCWDVCSXEXGWEXH
      MVOVRWLXGVOVPWLXGVQABCDNVGVHXGWEVISVFRUTOVJVLVK $.
      $( [4-Mar-2013] $)
  $}

  ${
    $( A strictly monotone ordinal function preserves weak ordering.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    smoword $p |- ( ( ( F Fn A /\ Smo F ) /\ ( C e. A /\ D e. A ) ) ->
      ( C C_ D <-> ( F ` C ) C_ ( F ` D ) ) ) $=
      ( wfn wsmo wa wcel wn cfv wb word ordelord syl11anc ordtri1 con0 smofvon2
      wss eloni 3syl smoord notbid ancom2s smodm2 adantr simprl simprr 3bitr4d
      simplr ) DAEZDFZGZBAHZCAHZGZGZCBHZIZCDJZBDJZHZIZBCRZUTUSRZULUNUMURVBKULUN
      UMGGUQVAACBDUAUBUCUPBLZCLZVCURKUPALZUMVEULVGUOADUDUEZULUMUNUFABMNUPVGUNVF
      VHULUMUNUGACMNBCONUPUTLZUSLZVDVBKUPUKUTPHVIUJUKUOUIZBDQUTSTUPUKUSPHVJVKCD
      QUSSTUTUSONUH $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d A y x $.  $d C x $.  $d F y x $.
    $( A strictly monotone ordinal function is greater than or equal to its
       argument.  (Contributed by Mario Carneiro, 28-Feb-2013.) $)
    smogt $p |- ( ( F Fn A /\ Smo F /\ C e. A ) -> C C_ ( F ` C ) ) $=
      ( vx vy wcel cfv wss wa cv wi id fveq2 sseq12d w3a word syl11anc wral imp
      com12 wfn wsmo wceq imbi2d con0 smodm2 3adant3 simp3 ordelord visset elon
      sylibr weq eleq1 3anbi3d imbi12d wel simpl1 simpl2 ordtr1 exp3acom23 sylc
      pm2.27 syl111anc ralimdva simp31 simp32 smofvon2 3ad2ant2 simp33 3adantr3
      eloni smoel2 3impa ordtr2 syl22anc 3expia 3expd 3impia dfss3 syl6ibr syld
      syl a1i tfis2 mpcom vtoclga ) CAUAZCUBZBAFZBBCGZHZWJWHWIIZWLWMDJZWNCGZHZK
      WMWLKDBAWNBUCZWPWLWMWQWNBWOWKWQLWNBCMNUDWMWNAFZWPWHWIWRWPWNUEFZWHWIWROZWP
      WTWNPZWSWTAPZWRXAWHWIXBWRACUFZUGZWHWIWRUHZAWNUIZQWNDUJUKULWTWPKZWHWIEJZAF
      ZOZXHXHCGZHZKZDEDEUMZWTXJWPXLXNWRXIWHWIWNXHAUNUOXNWNXHWOXKXNLWNXHCMNUPXME
      WNRZXGKWSWTXOWPWTXOXLEWNRZWPWTXMXLEWNWTEDUQZIWHWIXIXMXLKWHWIWRXQURWHWIWRX
      QUSWTXQXIWTXBWRXQXIKXDXEXBXQWRXIXHWNAUTVAVBSXJXLVCVDVEWTXPXHWOFZEWNRWPWTX
      LXREWNWTXQXLXRKZWHWIWRXQXSKWMWRXQXLXRWHWIWRXQXLOZXRWHWIXTOZXHPZWOPZXLXKWO
      FZXRYAXAXQYBYAXBWRXAWHWIXBXTXCUGWHWIWRXQXLVFXFQWHWIWRXQXLVGWNXHUIQYAWOUEF
      ZYCWIWHYEXTWNCVHVIWOVLWCWHWIWRXQXLVJWHWIXTYDWMWRXQYDXLAWNXHCVMVKVNYBYCIXL
      YDIXRXHXKWOVOSVPVQVRVSSVEEWNWOVTWAWBTWDWEWFVQTWGTVS $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( The range of a strictly monotone ordinal function dominates the domain.
       (Contributed by Mario Carneiro, 13-Mar-2013.) $)
    smorndom $p |- ( ( F : A --> B /\ Smo F /\ Ord B ) -> A C_ B ) $=
      ( vx wf wsmo word w3a cv wcel wa cfv wss wfn simpl1 ffn syl simpl2 smodm2
      syl11anc ordelord sylancom simpl3 simpr smogt syl111anc ffvelrn 3ad2antl1
      ordtr2 imp syl22anc ex ssrdv ) ABCEZCFZBGZHZDABUQDIZAJZURBJZUQUSKZURGZUPU
      RURCLZMZVCBJZUTUQUSAGZVBVACANZUOVFVAUNVGUNUOUPUSOABCPQZUNUOUPUSRZACSTAURU
      AUBUNUOUPUSUCVAVGUOUSVDVHVIUQUSUDAURCUEUFUNUOUSVEUPABURCUGUHVBUPKVDVEKUTU
      RVCBUIUJUKULUM $.
      $( [15-Mar-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.
    $( The strictly monotone ordinal functions are also epsilon isomorphisms of
       subclasses of ` On ` . $)
    smoiso2 $p |- ( ( Ord A /\ B C_ On ) ->
      ( ( F : A -onto-> B /\ Smo F ) <-> F Isom _E , _E ( A , B ) ) ) $=
      ( vx vy word con0 wss wa wfo cep cv wbr cfv wral sylan sylanbrc wcel fvex
      adantl wsmo wiso wf1o wb wf1 wf fof smo11 df-f1o wfn fofn wel smoord epel
      simpl epelc 3bitr4g r19.21aivva df-iso ex w3a isof1o f1ofo syl smoiso jca
      3ad2ant1 3expib com12 impbid ) AFZBGHZIZABCJZCUAZIZABKKCUBZVMVPVQVMVPIABC
      UCZDLZELZKMZVSCNZVTCNZKMZUDZEAODAOZVQVPVRVMVPABCUEZVNVRVNABCUFVOWGABCUGAB
      CUHPVNVOUOABCUIQTVPWFVMVNCAUJZVOWFABCUKWHVOIZWEDEAAWIVSARVTARIIDEULWBWCRW
      AWDAVSVTCUMDEUNWBWCVSCSVTCSUPUQURPTDEABKKCUSQUTVQVMVPVQVKVLVPVQVKVLVAVNVO
      VQVKVNVLVQVRVNABKKCVBABCVCVDVGABCVEVFVHVIVJ $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y B $.
    smo.1 $e |- dom A = B $.
    smo.2 $e |- Smo A $.
    $( A strictly monotonic function is always increasing.  Exercise 1 in
       [TakeutiZaring] p. 50.  It holds even when ` C ` is transfinite.
       (Contributed by Andrew Salmon, 23-Nov-2011.) $)
    smoge $p |- ( C e. B -> C C_ ( A ` C ) ) $=
      ( vy con0 wcel cfv wss ax-mp mpan wi c0 wceq eleq1 fveq2 sseq12d imbi12d
      id vx cdm eleq2i word wsmo smodm ordelon sylbir cv csuc 0ss a1i wtr ordtr
      wa wb sylib trsuc visset sucid smoel mp3an13 adantl smofvon ontr2 ancomsd
      treq exp3a sylan2 mpd eloni ordelsuc sylibd syl5bir com23 imim2d imp syl5
      ex pm2.43d wral wlim ralim ordeq mpbi ordtr1 expcom r19.21aiv ciun ss2iun
      cuni limuni uniiun syl6req sseq1d smoiun sylan2br sstr syl syl6bi imim12d
      com12 pm2.43 syl56 tfinds mpcom ) CGHZCBHZCCAIZJZXHCAUBZHZXGXKBCDUCXKUDZX
      LXGAUEZXMEAUFKZXKCUGLUHUAUIZBHZXPXPAIZJZMZNBHZNNAIZJZMFUIZBHZYDYDAIZJZMZY
      DUJZBHZYIYIAIZJZMZXHXJMUAFCXPNOZXQYAXSYCXPNBPYNXPNXRYBYNTXPNAQRSXPYDOZXQY
      EXSYGXPYDBPYOXPYDXRYFYOTXPYDAQRSXPYIOZXQYJXSYLXPYIBPYPXPYIXRYKYPTXPYIAQRS
      XPCOZXQXHXSXJXPCBPYQXPCXRXIYQTXPCAQRSYCYAYBUKULYDGHZYHYMYRYHUOZYJYLYJYEYS
      YMBUMZYJYEXMYTXOXMXKUMZYTXKUNXKBOZUUAYTUPDXKBVGKUQKBYDURLYRYHYEYMMYRYGYMY
      EYRYJYGYLYJYIXKHZYRYGYLMZXKBYIDUCYRUUCUUDYRUUCUOZYGYDYKHZYLUUEYFYKHZYGUUF
      MZUUCUUGYRXNUUCYDYIHUUGEYDFUSUTYIAYDVAVBVCUUCYRYKGHZUUGUUHMXNUUCUUIEYIAVD
      LZYRUUIUOZUUGYGUUFUUKYGUUGUUFYDYFYKVEVFVHVIVJUUCYRUUIUUFYLUPZUUJUUIYRYKUD
      UULYKVKYDYKGVLVIVIVMVSVNVOVPVQVRVTVSYHFXPWAYEFXPWAZYGFXPWAZMXPWBZXQXTMXTY
      EYGFXPWCUUOXQUUMUUNXTXQUUMMUUOXQYEFXPYDXPHZXQYEBUDZUUPXQUOYEMXMUUQXOUUBXM
      UUQUPDXKBWDKWEYDXPBWFKWGWHULUUNFXPYDWIZFXPYFWIZJZUUOXTFXPYDYFWJUUOUUTXPUU
      SJZXTUUOUURXPUUSUUOXPXPWKUURXPWLFXPWMWNWOXQUVAXSXQUUSXRJZUVAXSMXNXQUVBEXQ
      XNXPXKHUVBXKBXPDUCFXPAWPWQLUVAUVBXSXPUUSXRWRWGWSXBWTVRXAXQXSXCXDXEXF $.
      $( [23-Nov-2011] $)
  $}

