$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  Functions on ordinals; strictly monotone ordinal functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d y z B $.
    iunon.1 $e |- A e. _V $.
    iunon.2 $e |- B e. _V $.
    $( The indexed union of a set of ordinal numbers ` B ( x ) ` is an ordinal
       number. $)
    iunon $p |- ( A. x e. A B e. On -> U_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral ciun cv wceq wrex cab cuni dfiun2 wss wi wal abid
      hbra1 ax-17 ra4 eleq1a syl6 r19.23ad syl5bi 19.21aiv hbab1 dfss2f sylibr
      abrexex ssonunii syl syl5eqel ) CHIZABJZABCKFLZCMZABNZFOZPZHAFBCEQURVBHRZ
      VCHIURUSVBIZUSHIZSZFTVDURVGFVEVAURVFVAFUAURUTVFABUQABUBVFAUCURALBIUQUTVFS
      UQABUDCHUSUEUFUGUHUIFGVBHVAFGUJGLHIFUCUKULVBAFBCDUMUNUOUP $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    iinon.1 $e |- B e. _V $.
    $( The nonempty indexed intersection of a class of ordinal numbers
       ` B ( x ) ` is an ordinal number. $)
    iinon $p |- ( ( A. x e. A B e. On /\ A =/= (/) ) ->
                |^|_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral c0 wne wa ciin cv wceq wrex cab cint dfiin2 wss
      wi wal abid hbra1 ax-17 ra4 eleq1a syl6 r19.23ad syl5bi 19.21aiv hbab1
      dfss2f sylibr wex df-rex exbii excom 19.42v isseti mpbiran2 3bitr2ri n0
      abn0 3bitr4i oninton sylan2b sylan syl5eqel ) CGHZABIZBJKZLABCMENZCOZABPZ
      EQZRZGAEBCDSVKVPGTZVLVQGHZVKVMVPHZVMGHZUAZEUBVRVKWBEVTVOVKWAVOEUCVKVNWAAB
      VJABUDWAAUEVKANBHZVJVNWAUAVJABUFCGVMUGUHUIUJUKEFVPGVOEFULFNGHEUEUMUNVLVRV
      PJKZVSWCAUOZVOEUOZVLWDWFWCVNLZAUOZEUOWGEUOZAUOWEVOWHEVNABUPUQWGAEURWIWCAW
      IWCVNEUOWCVNEUSECDUTVAUQVBABVCVOEVDVEVPVFVGVHVI $.
      $( [17-May-2007] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x y S $.  $d x y F $.  $d x T $.
    onfununi.1 $e |- ( Lim y -> ( F ` y ) = U_ x e. y ( F ` x ) ) $.
    onfununi.2 $e |- ( ( x e. On /\ y e. On /\ x C_ y )
                  -> ( F ` x ) C_ ( F ` y ) ) $.
    $( A property of functions on ordinal numbers.  Generalization of Theorem
       Schema 8E of [Enderton] p. 218.  (Contributed by Eric Schmidt,
       26-May-2009.) $)
    onfununi $p |- ( ( S e. T /\ S C_ On /\ S =/= (/) )
                -> ( F ` U. S ) = U_ x e. S ( F ` x ) ) $=
      ( wcel con0 wss c0 wne w3a cuni cfv cv ciun wn wa wlim wceq word
      ssorduni ad2antrr wi nelneq wo elssuni adantl wb ssel eloni syl6 imp
      ordsseleq syl2an anabss1 mpbid ord con1d syl5 exp4b pm2.43d com23 ssrdv
      ssn0 sylan uniss syl orduniss adantr eqssd 3jca df-lim sylibr an32s
      3adantl1 ssonuni limeq fveq2 iuneq1 eqeq12d imbi12d vtoclg 3adant3 mpd
      wral wrex eluni2 anim1d onelon adantrd ordelss a1i syland 3jcad exp3a
      reximdvai syl5bi ssiun r19.21aiv iunss cbviunv syl6sseq 3ad2ant2 eqsstrd
      ex ssiun2s pm2.61d2 jcad sseq2 anbi2d sseq2d 3com12 3expib vtoclga
      sylsyld ) CDHZCIJZCKLZMZCNZEOZACAPZEOZQZYAYBCHZYCYFJZYAYGRZYHYAYISZYCAYBY
      EQZYFYJYBTZYCYKUAZXSXTYIYLXRXSYIXTYLXSYISZXTSZYBUBZYBKLZYBYBNZUAZMYLYOYPY
      QYSXSYPYIXTCUCZUDYNCYBJZXTYQYNACYBXSYIYDCHZYDYBHZUEXSUUBYIUUCXSUUBYIUUCUE
      XSUUBUUBYIUUCUUBYISYDYBUAZRXSUUBSZUUCYDYBCUFUUEUUCUUDUUEUUCUUDUUEYDYBJZUU
      CUUDUGZUUBUUFXSYDCUHZUIXSUUBUUFUUGUJZUUEYDUBZYPUUIXSXSUUBUUJXSUUBYDIHZUUJ
      CIYDUKZYDULUMUNYTYDYBUOUPUQURUSUTVAVBVCVDUNVEZCYBVFVGYNYSXTYNYBYRYNUUAYBY
      RJUUMCYBVHVIXSYRYBJZYIXSYPUUNYTYBVJVIVKVLVKVMYBVNVOVPVQYAYLYMUEZYIXRXSUUO
      XTXRXSUUOXRXSYBIHZUUOCDVRZBPZTZUUREOZAUURYEQZUAZUEUUOBYBIUURYBUAZUUSYLUVB
      YMUURYBVSUVCUUTYCUVAYKUURYBEVTZAUURYBYEWAWBWCFWDUMUNWEVKWFYAYKYFJZYIXSXRU
      VEXTXSYKBCUUTQZYFXSYEUVFJZAYBWGYKUVFJXSUVGAYBXSUUCYEUUTJZBCWHZUVGUUCYDUUR
      HZBCWHXSUVIBYDCWIXSUVJUVHBCXSUURCHZUVJUVHXSUVKUVJSZUUKUURIHZYDUURJZMUVHXS
      UVLUUKUVMUVNXSUVLUVMUVJSUUKXSUVKUVMUVJCIUURUKZWJUURYDWKUMXSUVKUVMUVJUVOWL
      XSUVKUURUBZUVJUVNXSUVKUVMUVPUVOUURULUMUVPUVJSUVNUEXSUURYDWMWNWOWPGUMWQWRW
      SBCUUTYEWTUMXAAYBYEUVFXBVOBACUUTYEUURYDEVTXCXDXEVKXFXGACYEYBYCYDYBEVTXHXI
      YAYEYCJZACWGYFYCJYAUVQACYAUUPUUBUUKUUFSZUVQXRXSUUPXTXRXSUUPUUQUNWEYAUUBUU
      KUUFXSXRUUBUUKUEXTUULXEUUBUUFUEYAUUHWNXJUUKUVNSZUVHUEUVRUVQUEBYBIUVCUVSUV
      RUVHUVQUVCUVNUUFUUKUURYBYDXKXLUVCUUTYCYEUVDXMWCUVMUUKUVNUVHUUKUVMUVNUVHGX
      NXOXPXQXAACYEYCXBVOVL $.
      $( [26-May-2009] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.  $d w x y z K $.  $d w x y L $.
    $d w x y z S $.
    onopruni.1 $e |- ( Lim y -> ( A F y ) = U_ x e. y ( A F x ) ) $.
    onopruni.2 $e |- ( ( x e. On /\ y e. On /\ x C_ y )
                  -> ( A F x ) C_ ( A F y ) ) $.
    ${
      $d x T $.
      $( A variant of ~ onfununi for operations.  (Contributed by Eric Schmidt,
         26-May-2009.) $)
      onopruni $p |- ( ( S e. T /\ S C_ On /\ S =/= (/) )
                     -> ( A F U. S ) = U_ x e. S ( A F x ) ) $=
        ( vw vz wcel con0 wss c0 wne w3a cuni cv co wceq copab cfv ciun wlim
        visset oprex opreq2 fvopab a1i iuneq2i 3eqtr4g 3sstr4g onfununi cvv
        uniexg fvopabg sylancl 3ad2ant1 3eqtr3d ) DEKZDLMZDNOZPZDQZIRCJRZFSZTJI
        UAZUBZADARZVGUBZUCZCVDFSZADCVIFSZUCZABDEVGBRZUDCVOFSZAVOVMUCVOVGUBZAVOV
        JUCGJIVOVFVPBUECVOFUFVEVOCFUGUHZAVOVJVMVJVMTZVIVOKJIVIVFVMAUECVIFUFVEVI
        CFUGUHZUIUJUKVILKVOLKVIVOMPVMVPVJVQHVTVRULUMUTVAVHVLTZVBUTVDUNKVLUNKWAD
        EUOCVDFUFJIVDVFVLUNUNVEVDCFUGUPUQURVKVNTVCADVJVMVSVIDKVTUIUJUIUS $.
        $( [26-May-2009] $)
    $}

    $( A variant of ~ onopruni with indexed unions.  (Contributed by Eric
       Schmidt, 26-May-2009.)  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 19-Jun-2012.) $)
    onopriun $p |- ( ( K e. T /\ A. z e. K L e. On /\ K =/= (/) )
                -> ( A F U_ z e. K L ) = U_ z e. K ( A F L ) ) $=
      ( vw wcel con0 wral c0 wne w3a ciun co cv wceq wrex cab cuni dfiun2g
      opreq2d 3ad2ant2 cvv wss abrexexg 3ad2ant1 hbra1 ax-17 ra4 eleq1 biimprd
      syl9 com23 r19.23ad abssdv wa wex r19.2z elex reximi syl rexcom4 sylib
      abn0 sylibr ancoms 3adant1 onopruni syl111anc visset eqeq1 rexbidv elab
      anbi1i r19.41v bitr4i exbii df-rex 3bitr4i wb imp opreq2 eqeq2d ceqsexgv
      rexbida syl5bb abbidv unieqd oprex dfiun2 3eqtr4g 3eqtrd ) GELZHMLZCGNZGO
      PZQZDCGHRZFSZDKTZHUAZCGUBZKUCZUDZFSZAXHDATZFSZRZCGDHFSZRZWTWRXDXJUAXAWTXC
      XIDFCKGHMUEUFUGXBXHUHLZXHMUIZXHOPZXJXMUAWRWTXPXACKGHEUJUKWTWRXQXAWTXGKMWT
      XFXEMLZCGWSCGULZXSCUMWTXFCTGLZXSWTYAWSXFXSWSCGUNZXFXSWSXEHMUOUPUQURUSUTUG
      WTXAXRWRXAWTXRXAWTVAZXGKVBZXRYCXFKVBZCGUBZYDYCWSCGUBYFWSCGVCWSYECGKHMVDVE
      VFXFCKGVGVHXGKVIVJVKVLABDXHUHFIJVMVNWTWRXMXOUAXAWTBTZXLUAZAXHUBZBUCZUDYGX
      NUAZCGUBZBUCZUDXMXOWTYJYMWTYIYLBYIXKHUAZYHVAZAVBZCGUBZWTYLXKXHLZYHVAZAVBY
      OCGUBZAVBYIYQYSYTAYSYNCGUBZYHVAYTYRUUAYHXGUUAKXKAVOXEXKUAXFYNCGXEXKHVPVQV
      RVSYNYHCGVTWAWBYHAXHWCYOCAGVGWDWTYPYKCGXTWTYAVAWSYPYKWEWTYAWSYBWFYHYKAHMY
      NXLXNYGXKHDFWGWHWIVFWJWKWLWMABXHXLDXKFWNWOCBGXNDHFWNWOWPUGWQ $.
      $( [24-Jul-2012] $) $( [26-May-2009] $)
  $}

  $c Smo $.

  $( Introduce the strictly monotone ordinal function.  A strictly monotone
     function is one that is constantly increasing across the ordinals. $)
  csmo $a wff Smo A $.

  ${
    $d x y A $.
    $( Definition of a strictly monotone ordinal function.  Definition 7.46 in
       [TakeutiZaring] p. 50. $)
    df-smo $a |- ( Smo A <-> ( A : dom A --> On /\ Ord dom A /\ A. x e. dom A
       A. y e. dom A ( x e. y -> ( A ` x ) e. ( A ` y ) ) ) ) $.
  $}

  ${
    $d F x y $.
    $( Alternate definition of a strictly monotone ordinal function.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    dfsmo2 $p |- ( Smo F <-> ( F : dom F --> On /\ Ord dom F /\ A. x e. dom F
       A. y e. x ( F ` y ) e. ( F ` x ) ) ) $=
      ( csmo cdm con0 wf word wel cv cfv wcel wi wral w3a df-smo wa ralcom
      impexp simpr ordtr1 3impib 3com23 simp3 jca 3expia impbid2 imbi1d
      syl5bbr ralbidv2 ralbidva syl5bb pm5.32i anbi2i 3anass 3bitr4i bitri ) CD
      CEZFCGZURHZBAIZBJZCKAJZCKLZMZAURNBURNZOZUSUTVDBVCNZAURNZOZBACPUSUTVFQZQUS
      UTVIQZQVGVJVKVLUSUTVFVIVFVEBURNZAURNUTVIVEBAURURRUTVMVHAURUTVCURLZQZVEVDB
      URVCVBURLZVEMVPVAQZVDMVOVEVPVAVDSVOVQVAVDVOVQVAVPVATUTVNVAVQUTVNVAOVPVAUT
      VAVNVPUTVAVNVPVBVCURUAUBUCUTVNVAUDUEUFUGUHUIUJUKULUMUNUSUTVFUOUSUTVIUOUPU
      Q $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d x y A $.
    issmo.1 $e |- A : B --> On $.
    issmo.2 $e |- Ord B $.
    issmo.3 $e |- ( ( x e. B /\ y e. B ) -> ( x e. y -> ( A ` x ) e. ( A ` y
       ) ) ) $.
    issmo.4 $e |- dom A = B $.
    $( Conditions for which ` A ` is a strictly monotone ordinal function.
       (Contributed by Andrew Salmon, 15-Nov-2011.) $)
    issmo $p |- Smo A $=
      ( csmo cdm con0 wf word cv wcel cfv wi wral df-smo feq2i mpbir wceq wb
      ordeq ax-mp eleq2i syl2anb rgen2a mpbir3an ) CICJZKCLZUJMZANZBNZOUMCPUNCP
      OQZBUJRAUJRABCSUKDKCLEUJDKCHTUAULDMZFUJDUBULUPUCHUJDUDUEUAUOABUJUMUJOUMDO
      UNDOUOUNUJOUJDUMHUFUJDUNHUFGUGUHUI $.
      $( [15-Nov-2011] $)
  $}

  ${
    $d A x $.  $d F x y $.
    $( Alternative definition of a strictly monotone ordinal function.
       (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    issmo2 $p |- ( F : A --> B -> ( ( B C_ On /\ Ord A /\
       A. x e. A A. y e. x ( F ` y ) e. ( F ` x ) ) -> Smo F ) ) $=
      ( wf con0 wss word cv cfv wcel wral w3a cdm csmo fss ex fdm feq2d
      sylibrd wceq wb ordeq syl biimprd raleqdv 3anim123d dfsmo2 syl6ibr ) CDEF
      ZDGHZCIZBJEKAJZEKLBUNMZACMZNEOZGEFZUQIZUOAUQMZNEPUKULURUMUSUPUTUKULCGEFZU
      RUKULVACDGEQRUKUQCGECDESZTUAUKUSUMUKUQCUBUSUMUCVBUQCUDUEUFUKUTUPUKUOAUQCV
      BUGUFUHABEUIUJ $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for strictly monotone functions.  (Contributed by
       Andrew Salmon, 16-Nov-2011.) $)
    smoeq $p |- ( A = B -> ( Smo A <-> Smo B ) ) $=
      ( vx vy wceq cdm con0 wf word cv wcel cfv wi wral w3a csmo id dmeq
      feq12d wb ordeq syl fveq1 eleq12d imbi2d 2ralbidv raleq ralbidv 3bitrd
      3anbi123d df-smo 3bitr4g ) ABEZAFZGAHZUNIZCJZDJZKZUQALZURALZKZMZDUNNCUNNZ
      OBFZGBHZVEIZUSUQBLZURBLZKZMZDVENZCVENZOAPBPUMUOVFUPVGVDVMUMUNVEGABUMQABRZ
      SUMUNVEEZUPVGTVNUNVEUAUBUMVDVKDUNNZCUNNVLCUNNZVMUMVCVKCDUNUNUMVBVJUSUMUTV
      HVAVIUQABUCURABUCUDUEUFUMVPVLCUNUMVOVPVLTVNVKDUNVEUGUBUHUMVOVQVMTVNVLCUNV
      EUGUBUIUJCDAUKCDBUKUL $.
      $( [16-Nov-2011] $)

    $( The domain of a strictly monotone function is an ordinal.  (Contributed
       by Andrew Salmon, 16-Nov-2011.) $)
    smodm $p |- ( Smo A -> Ord dom A ) $=
      ( vx vy csmo cdm con0 wf word cv wcel cfv wi wral w3a df-smo simp2 sylbi
      ) ADAEZFAGZRHZBIZCIZJUAAKUBAKJLCRMBRMZNTBCAOSTUCPQ $.
      $( [16-Nov-2011] $)

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone.  (Contributed by Andrew Salmon, 16-Nov-2011.) $)
    smores $p |- ( ( Smo A /\ B e. dom A ) -> Smo ( A |` B ) ) $=
      ( vx vy cdm wcel csmo cres con0 wf word cv cfv wi wral w3a wfn crn wss
      wa wfun funres funfn 3imtr3i resss rnss ax-mp sstr mpan anim12i df-f
      3imtr4i a1i cin ordelord expcom ordin ex syli wceq wb dmres ordeq
      syl6ibr hbra1 hbra2 ax-17 eleq2i anbi12i elin ra42 pm2.27 fvres adantr
      adantl eleq12d biimprcd imim2i com23 syl6 impcom syl5 an4s com12 syl5bi
      exp3a r19.21ad r19.21ai 3anim123d df-smo 3imtr4g ) BAEZFZAGZABHZGZWMWLIAJ
      ZWLKZCLZDLZFZWSAMZWTAMZFZNZDWLOZCWLOZPWOEZIWOJZXHKZXAWSWOMZWTWOMZFZNZDXHO
      ZCXHOZPWNWPWMWQXIWRXJXGXPWQXINWMAWLQZARZISZTWOXHQZWORZISZTWQXIXQXTXSYBAUA
      WOUAXQXTBAUBAUCWOUCUDYAXRSZXSYBWOASYCABUEWOAUFUGYAXRIUHUIUJWLIAUKXHIWOUKU
      LUMWMWRBWLUNZKZXJWRWMBKZYEWRWMYFWLBUOUPYFWRYEBWLUQURUSXHYDUTXJYEVAABVBZXH
      YDVCUGVDXGXPNWMXGXOCXHXFCWLVEXGWSXHFZXNDXHXECDWLWLVFYHDVGXGYHWTXHFZXNYHYI
      TWSYDFZWTYDFZTZXGXNYHYJYIYKXHYDWSYGVHXHYDWTYGVHVIYLWSBFZWSWLFZTZWTBFZWTWL
      FZTZTZXGXNYJYOYKYRWSBWLVJWTBWLVJVIYSXGXNYMYPYNYQXGXNNXGYNYQTZXENZYMYPTZYT
      TXNXECDWLWLVKYTUUBUUAXNNYTUUAUUBXNYTUUAXEUUBXNNYTXEVLXEXAUUBXMXDUUBXMNXAU
      UBXMXDUUBXKXBXLXCYMXKXBUTYPWSBAVMVNYPXLXCUTYMWTBAVMVOVPVQVRVSVTVSWAWBWCWD
      WEWEWFWGWHUMWICDAWJCDWOWJWKWA $.
      $( [16-Nov-2011] $)

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone.  (Contributed by Andrew Salmon, 19-Nov-2011.) $)
    smores3 $p |- ( ( Smo ( A |` B ) /\ C e. ( dom A i^i B ) /\ Ord B ) -> Smo
       ( A |` C ) ) $=
      ( cres csmo cdm cin wcel word w3a dmres incom eqtri eleq2i smores
      sylan2br 3adant3 wss wceq wb inss2 sseli ordelss ancoms sylan 3adant1
      resabs1 smoeq 3syl mpbid ) ABDZEZCAFZBGZHZBIZJZUKCDZEZACDZEZULUOUSUPUOULC
      UKFZHUSVBUNCVBBUMGUNABKBUMLMNUKCOPQUQCBRZURUTSUSVATUOUPVCULUOCBHZUPVCUNBC
      UMBUAUBUPVDVCBCUCUDUEUFACBUGURUTUHUIUJ $.
      $( [19-Nov-2011] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    $( A strictly monotone ordinal function restricted to an ordinal is still
       monotone. $)
    smores2 $p |- ( ( Smo F /\ Ord A ) -> Smo ( F |` A ) ) $=
      ( vy vx csmo word wa cres cdm con0 wf cv cfv wcel wral wfn crn wss wfun
      dfsmo2 simp1bi ffun funres funfn sylib 3syl frn cima df-ima imassrn
      eqsstr3i sstr mpan df-f sylanbrc adantr smodm cin ordin wceq wb dmres
      ordeq ax-mp sylibr ancoms sylan resss dmss simp3bi ssralv mpsyl wel wi
      ordtr1 syl inss1 eqsstri sseli syl6 exp3acom23 imp31 fvres ad2antlr
      eleq12d ralbidva mpbird w3a biimpri syl111anc ) BEZAFZGZBAHZIZJWNKZWOFZCL
      ZWNMZDLZWNMZNZCWTOZDWOOZWNEZWKWPWLWKWNWOPZWNQZJRZWPWKBIZJBKZBSZXFWKXJXIFZ
      WRBMZWTBMZNZCWTOZDXIOZDCBTZUAZXIJBUBXKWNSXFABUCWNUDUEUFWKXJBQZJRZXHXSXIJB
      UGXGXTRYAXHXGBAUHXTBAUIBAUJUKXGXTJULUMUFWOJWNUNUOUPWKXLWLWQBUQWLXLWQWLXLG
      AXIURZFZWQAXIUSWOYBUTWQYCVABAVBZWOYBVCVDVEVFVGZWMXDXPDWOOZWKYFWLWOXIRZWKX
      QYFWNBRYGBAVHWNBVIVDWKXJXLXQXRVJXPDWOXIVKVLUPWMXCXPDWOWMWTWONZGZXBXOCWTYI
      CDVMZGZWSXMXAXNYKWRANZWSXMUTWMYHYJYLWMYJYHYLWMYJYHGZWRWONZYLWMWQYMYNVNYEW
      RWTWOVOVPWOAWRWOYBAYDAXIVQVRZVSVTWAWBWRABWCVPYHXAXNUTZWMYJYHWTANYPWOAWTYO
      VSWTABWCVPWDWEWFWFWGXEWPWQXDWHDCWNTWIWJ $.
      $( [15-Mar-2013] $)
  $}

  $( The domain of a strictly monotone ordinal function is an ordinal.
     (Contributed by Mario Carneiro, 12-Mar-2013.) $)
  smodm2 $p |- ( ( F Fn A /\ Smo F ) -> Ord A ) $=
    ( csmo wfn cdm word smodm wceq wb fndm ordeq syl biimpa sylan2 ) BCBADZBEZF
    ZAFZBGOQROPAHQRIABJPAKLMN $.
    $( [12-Mar-2013] $)

  ${
    $d F x y $.
    $( The function values of a strictly monotone ordinal function are
       ordinals.  (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    smofvon2 $p |- ( Smo F -> ( F ` B ) e. On ) $=
      ( vy vx cdm wcel csmo cfv con0 wi wf word cv wral dfsmo2 simp1bi ffvelrn
      expcom syl5 wn c0 ndmfv 0elon syl6eqel a1d pm2.61i ) ABEZFZBGZABHZIFZJUIU
      GIBKZUHUKUIULUGLCMBHDMZBHFCUMNDUGNDCBOPULUHUKUGIABQRSUHTZUKUIUNUJUAIABUBU
      CUDUEUF $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d x y A $.
    iordsmo.1 $e |- Ord A $.
    $( The identity relation restricted to the ordinals is a strictly monotone
       function.  (Contributed by Andrew Salmon, 16-Nov-2011.) $)
    iordsmo $p |- Smo ( _I |` A ) $=
      ( vx vy cid cres con0 wf wfn crn wss df-f fnresi rnresi word ordsson
      ax-mp eqsstri mpbir2an cv wcel wa cfv wceq fvresi adantr adantl eleq12d
      biimprd dmresi issmo ) CDEAFZAAGULHULAIULJZGKAGULLAMUMAGANAOAGKBAPQRSBCTZ
      AUAZDTZAUAZUBZUNULUCZUPULUCZUAUNUPUAURUSUNUTUPUOUSUNUDUQAUNUEUFUQUTUPUDUO
      AUPUEUGUHUIAUJUK $.
      $( [16-Nov-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y F $.
    $( The null set is a strictly monotone ordinal function.  (Contributed by
       Andrew Salmon, 20-Nov-2011.) $)
    smo0 $p |- Smo (/) $=
      ( cid c0 cres csmo ord0 iordsmo wceq wb res0 smoeq ax-mp mpbi ) ABCZDZBDZ
      BEFMBGNOHAIMBJKL $.
      $( [20-Nov-2011] $)

    $( If ` B ` is a strictly monotone ordinal function, and ` A ` is in the
       domain of ` B ` , then the value of the function at ` A ` is an
       ordinal.  (Contributed by Andrew Salmon, 20-Nov-2011.) $)
    smofvon $p |- ( ( Smo B /\ A e. dom B ) -> ( B ` A ) e. On ) $=
      ( vx vy csmo cdm con0 wf wcel cfv word cv wi wral w3a df-smo simp1 sylbi
      ffvelrn sylan ) BEZBFZGBHZAUBIABJGIUAUCUBKZCLZDLZIUEBJUFBJIMDUBNCUBNZOUCC
      DBPUCUDUGQRUBGABST $.
      $( [20-Nov-2011] $)

    $( If ` x ` is less than ` y ` then a strictly monotone function's value
       will be strictly less at ` x ` than at ` y ` .  (Contributed by Andrew
       Salmon, 22-Nov-2011.) $)
    smoel $p |- ( ( Smo B /\ A e. dom B /\ C e. A ) -> ( B ` C ) e. ( B ` A )
       ) $=
      ( vx vy csmo cdm wcel cfv wa wi word smodm ordtr1 ancomsd expdimp sylan
      con0 wf cv wral w3a df-smo wceq eleq1 fveq2 eleq1d imbi12d eleq2 eleq2d
      rcla42v ancoms com12 3ad2ant3 sylbi syld pm2.43d 3impia ) BFZABGZHZCAHZCB
      IZABIZHZUSVAJZVBVEVFVBCUTHZVBVEKZUSUTLZVAVBVGKBMVIVAVBVGVIVBVAVGCAUTNOPQU
      SVAVGVHUSUTRBSZVIDTZETZHZVKBIZVLBIZHZKZEUTUADUTUAZUBVAVGJZVHKZDEBUCVRVJVT
      VIVSVRVHVGVAVRVHKVQVHCVLHZVCVOHZKDECAUTUTVKCUDZVMWAVPWBVKCVLUEWCVNVCVOVKC
      BUFUGUHVLAUDZWAVBWBVEVLACUIWDVOVDVCVLABUFUJUHUKULUMUNUOPUPUQUR $.
      $( [22-Nov-2011] $)

    $( The value of a strictly monotone ordinal function contains its indexed
       union.  (Contributed by Andrew Salmon, 22-Nov-2011.) $)
    smoiun $p |- ( ( Smo B /\ A e. dom B ) -> U_ x e. A ( B ` x )
       C_ ( B ` A ) ) $=
      ( vy csmo cdm wcel wa cv cfv ciun wrex eliun con0 wi smofvon smoel
      3expia ontr1 ancomsd exp3a sylsyld r19.23adv syl5bi ssrdv ) CEZBCFGZHZDAB
      AIZCJZKZBCJZDIZUKGUMUJGZABLUHUMULGZAUMBUJMUHUNUOABUHULNGZUIBGZUJULGZUNUOO
      BCPUFUGUQURBCUIQRUPURUNUOUPUNURUOUMUJULSTUAUBUCUDUE $.
      $( [22-Nov-2011] $)

    $( If ` F ` is an isomorphism from an ordinal ` A ` onto ` B ` , which is a
       subset of the ordinals, then ` F ` is a strictly monotonic function.
       Exercise 3 in [TakeutiZaring] p. 50.  (Contributed by Andrew Salmon,
       24-Nov-2011.) $)
    smoiso $p |- ( ( F Isom _E , _E ( A , B ) /\ Ord A /\ B C_ On ) -> Smo F )
        $=
      ( vx vy cep wiso word con0 wss w3a cdm wf cv wcel cfv wi wral csmo wf1o
      isof1o f1of syl ffdm simpld fss sylan 3adant2 syl3an1 wceq wb fdm eqcomd
      3syl ordeq biimpa 3adant3 wa eleq2d anbi12d wbr isorel epel fvex epelc
      3bitr3g biimpd ex sylbid r19.21aivv 3ad2ant1 3jca df-smo sylibr ) ABFFCGZ
      AHZBIJZKZCLZICMZVSHZDNZENZOZWBCPZWCCPZOZQZEVSRDVSRZKCSVRVTWAWIVOABCMZVPVQ
      VTVOABCTZWJABFFCUAZABCUBZUCWJVQVTVPWJVSBCMZVQVTWJWNVSAJABCUDUEVSBICUFUGUH
      UIVOVPWAVQVOVPWAVOAVSUJZVPWAUKVOWKWJWOWLWMWJVSAABCULZUMUNAVSUOUCUPUQVOVPW
      IVQVOWHDEVSVSVOWBVSOZWCVSOZURZWBAOZWCAOZURZWHVOWKWJWSXBUKWLWMWJWQWTWRXAWJ
      VSAWBWPUSWJVSAWCWPUSUTUNVOXBWHVOXBURZWDWGXCWBWCFVAWEWFFVAWDWGABWBWCFFCVBD
      EVCWEWFWBCVDWCCVDVEVFVGVHVIVJVKVLDECVMVN $.
      $( [24-Nov-2011] $)
  $}

  ${
    $d A w x z $.  $d B w z $.  $d F w x y z $.
    $( A strictly monotone ordinal function preserves the epsilon relation.
       (Contributed by Mario Carneiro, 12-Mar-2013.) $)
    smoel2 $p |- ( ( ( F Fn A /\ Smo F ) /\ ( B e. A /\ C e. B ) ) ->
      ( F ` C ) e. ( F ` B ) ) $=
      ( wfn csmo wa wcel cfv cdm fndm eleq2d anbi1d biimprd smoel 3expib
      sylan9 imp ) DAEZDFZGBAHZCBHZGZCDIBDIHZSUCBDJZHZUBGZTUDSUGUCSUFUAUBSUEABA
      DKLMNTUFUBUDBDCOPQR $.
      $( [12-Mar-2013] $)
  $}

  ${
    $d A w x y z $.  $d F w x y z $.
    $( A strictly monotone ordinal function is one-to-one.  (Contributed by
       Mario Carneiro, 28-Feb-2013.) $)
    smo11 $p |- ( ( F : A --> B /\ Smo F ) -> F : A -1-1-> B ) $=
      ( vz vw vy vx wf csmo wa cv cfv wceq weq wi wral wf1 simpl wfn ffn wcel
      word smodm2 ordelord ex syl anim12d wel w3o ordtri3or w3a simp1rr smoel2
      r19.21aivva adantr 3ad2ant1 simp2 simp3 fveq2 eleq2d raleqbi1dv rcla4v
      eleq1d rcla4cv syl6 3imp eleq1 biimpac sylan syl31anc wn con0 smofvon2
      eloni ordirr 3syl ad2antlr pm2.65i pm2.21i 3exp ax-1 a1i simp1rl eleq2
      3jaod syl5 mpdd r19.21aivv dff13 sylanbrc ) ABCHZCIZJWKDKZCLZEKZCLZMZDENZ
      OZEAPDAPZABCQWKWLRWKCASZWLWTABCTXAWLJZWSDEAAXBWMAUAZWOAUAZJZWMUBZWOUBZJZW
      SXBXCXFXDXGXBAUBZXCXFOACUCZXIXCXFAWMUDUEUFXBXIXDXGOXJXIXDXGAWOUDUEUFUGXBX
      EXHWSOXHDEUHZWREDUHZUIXBXEJZWSWMWOUJXMXKWSWRXLXMXKWQWRXMXKWQUKZWRXNWPWPUA
      ZXNXDFKZCLZGKZCLZUAZFXRPZGAPZXKWQXOXCXDXBXKWQULXMXKYBWQXBYBXEXBXTGFAXRAXR
      XPCUMUNUOZUPXMXKWQUQXMXKWQURXDYBXKUKWNWPUAZWQXOXDYBXKYDXDYBXQWPUAZFWOPZXK
      YDOYAYFGWOAXTYEFXRWOGENXSWPXQXRWOCUSUTVAVBYEYDFWMWOFDNXQWNWPXPWMCUSVCVDVE
      VFWQYDXOWNWPWPVGVHVIVJXMXKXOVKZWQWLYGXAXEWLWPVLUAWPUBYGWOCVMWPVNWPVOVPVQZ
      UPVRVSVTWRWSOXMWRWQWAWBXMXLWQWRXMXLWQUKZWRYIXOYIXCYBXLWQXOXCXDXBXLWQWCXMX
      LYBWQYCUPXMXLWQUQXMXLWQURXCYBXLUKWPWNUAZWQXOXCYBXLYJXCYBXQWNUAZFWMPZXLYJO
      YAYLGWMAXTYKFXRWMGDNXSWNXQXRWMCUSUTVAVBYKYJFWOWMFENXQWPWNXPWOCUSVCVDVEVFW
      QYJXOWNWPWPWDVHVIVJXMXLYGWQYHUPVRVSVTWEWFUEWGWHVIDEABCWIWJ $.
      $( [28-Feb-2013] $)
  $}

  ${
    $( A strictly monotone ordinal function preserves strict ordering.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    smoord $p |- ( ( ( F Fn A /\ Smo F ) /\ ( C e. A /\ D e. A ) ) ->
      ( C e. D <-> ( F ` C ) e. ( F ` D ) ) ) $=
      ( wfn csmo wa wcel word cfv wb smodm2 adantr simprl ordelord syl11anc
      simprr wceq w3o ordtri3or w3a simp3 wi smoel2 expr adantrl 3impia 2thd
      3expia wn ordirr syl 3adant3 eleq2d mtbid con0 smofvon2 ad2antlr eloni
      3syl fveq2d con4bid ordn2lp pm3.2 3ad2ant3 mtod adantrlr 3impb pm3.21
      3jaod syl5 mp2and ) DAEZDFZGZBAHZCAHZGZGZBIZCIZBCHZBDJZCDJZHZKZVSAIZVPVTV
      OWGVRADLMZVOVPVQNABOPZVSWGVQWAWHVOVPVQQACOPZVTWAGWBBCRZCBHZSVSWFBCTVSWBWF
      WKWLVOVRWBWFVOVRWBUAWBWEVOVRWBUBVOVRWBWEVOVQWBWEUCVPVOVQWBWEACBDUDUEUFUGU
      HUIVOVRWKWFVOVRWKUAZWBWEWMWBUJZWEUJZWMBBHZWBVOVRWPUJZWKVSVTWQWIBUKULUMWMB
      CBVOVRWKUBZUNUOWMWCWCHZWEVOVRWSUJZWKVSWCUPHZWCIZWTVNXAVMVRBDUQURZWCUSZWCU
      KUTUMWMWCWDWCWMBCDWRVAUNUOUHVBUIVOVRWLWFVOVRWLUAZWBWEXEWNWOXEWBWLWBGZXEWA
      XFUJVOVRWAWLWJUMCBVCULWLVOWBXFUCVRWLWBVDVEVFXEWEWEWDWCHZGZXEXBXHUJVOVRXBW
      LVSXAXBXCXDULUMWCWDVCULXEXGWEXHUCVOVRWLXGVOVPWLXGVQABCDUDVGVHXGWEVIULVFUH
      VBUIVJVKVL $.
      $( [4-Mar-2013] $)
  $}

  ${
    $( A strictly monotone ordinal function preserves weak ordering.
       (Contributed by Mario Carneiro, 4-Mar-2013.) $)
    smoword $p |- ( ( ( F Fn A /\ Smo F ) /\ ( C e. A /\ D e. A ) ) ->
      ( C C_ D <-> ( F ` C ) C_ ( F ` D ) ) ) $=
      ( wfn csmo wa wcel wn cfv wss wb smoord notbid ancom2s word smodm2
      adantr simprl ordelord syl11anc simprr ordtri1 con0 simplr smofvon2
      eloni 3syl 3bitr4d ) DAEZDFZGZBAHZCAHZGZGZCBHZIZCDJZBDJZHZIZBCKZUTUSKZULU
      NUMURVBLULUNUMGGUQVAACBDMNOUPBPZCPZVCURLUPAPZUMVEULVGUOADQRZULUMUNSABTUAU
      PVGUNVFVHULUMUNUBACTUABCUCUAUPUTPZUSPZVDVBLUPUKUTUDHVIUJUKUOUEZBDUFUTUGUH
      UPUKUSUDHVJVKCDUFUSUGUHUTUSUCUAUI $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d A y x $.  $d C x $.  $d F y x $.
    $( A strictly monotone ordinal function is greater than or equal to its
       argument.  (Contributed by Mario Carneiro, 28-Feb-2013.) $)
    smogt $p |- ( ( F Fn A /\ Smo F /\ C e. A ) -> C C_ ( F ` C ) ) $=
      ( vx vy wfn csmo wcel cfv wss wa cv wi wceq id fveq2 sseq12d imbi2d con0
      w3a word smodm2 3adant3 simp3 ordelord syl11anc visset elon sylibr weq
      eleq1 3anbi3d imbi12d wral wel simpl1 simpl2 ordtr1 exp3acom23 sylc imp
      pm2.27 syl111anc ralimdva simp31 simp32 smofvon2 3ad2ant2 eloni syl
      simp33 smoel2 3adantr3 3impa ordtr2 syl22anc 3expia 3expd 3impia dfss3
      syl6ibr syld com12 a1i tfis2 mpcom vtoclga ) CAFZCGZBAHZBBCIZJZWJWHWIKZWL
      WMDLZWNCIZJZMWMWLMDBAWNBNZWPWLWMWQWNBWOWKWQOWNBCPQRWMWNAHZWPWHWIWRWPWNSHZ
      WHWIWRTZWPWTWNUAZWSWTAUAZWRXAWHWIXBWRACUBZUCZWHWIWRUDZAWNUEZUFWNDUGUHUIWT
      WPMZWHWIELZAHZTZXHXHCIZJZMZDEDEUJZWTXJWPXLXNWRXIWHWIWNXHAUKULXNWNXHWOXKXN
      OWNXHCPQUMXMEWNUNZXGMWSWTXOWPWTXOXLEWNUNZWPWTXMXLEWNWTEDUOZKWHWIXIXMXLMWH
      WIWRXQUPWHWIWRXQUQWTXQXIWTXBWRXQXIMXDXEXBXQWRXIXHWNAURUSUTVAXJXLVBVCVDWTX
      PXHWOHZEWNUNWPWTXLXREWNWTXQXLXRMZWHWIWRXQXSMWMWRXQXLXRWHWIWRXQXLTZXRWHWIX
      TTZXHUAZWOUAZXLXKWOHZXRYAXAXQYBYAXBWRXAWHWIXBXTXCUCWHWIWRXQXLVEXFUFWHWIWR
      XQXLVFWNXHUEUFYAWOSHZYCWIWHYEXTWNCVGVHWOVIVJWHWIWRXQXLVKWHWIXTYDWMWRXQYDX
      LAWNXHCVLVMVNYBYCKXLYDKXRXHXKWOVOVAVPVQVRVSVAVDEWNWOVTWAWBWCWDWEWFVQWCWGW
      CVS $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( The range of a strictly monotone ordinal function dominates the domain.
       (Contributed by Mario Carneiro, 13-Mar-2013.) $)
    smorndom $p |- ( ( F : A --> B /\ Smo F /\ Ord B ) -> A C_ B ) $=
      ( vx wf csmo word w3a cv wcel wa cfv wss wfn simpl1 ffn syl simpl2
      smodm2 syl11anc ordelord sylancom simpl3 simpr smogt syl111anc ffvelrn
      3ad2antl1 ordtr2 imp syl22anc ex ssrdv ) ABCEZCFZBGZHZDABUQDIZAJZURBJZUQU
      SKZURGZUPURURCLZMZVCBJZUTUQUSAGZVBVACANZUOVFVAUNVGUNUOUPUSOABCPQZUNUOUPUS
      RZACSTAURUAUBUNUOUPUSUCVAVGUOUSVDVHVIUQUSUDAURCUEUFUNUOUSVEUPABURCUGUHVBU
      PKVDVEKUTURVCBUIUJUKULUM $.
      $( [15-Mar-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.
    $( The strictly monotone ordinal functions are also epsilon isomorphisms of
       subclasses of ` On ` . $)
    smoiso2 $p |- ( ( Ord A /\ B C_ On ) ->
      ( ( F : A -onto-> B /\ Smo F ) <-> F Isom _E , _E ( A , B ) ) ) $=
      ( vx vy word con0 wss wa wfo csmo cep wiso wf1o cv wbr cfv wb wral wf1
      wf fof smo11 sylan simpl df-f1o sylanbrc adantl wfn fofn wcel wel smoord
      epel fvex epelc 3bitr4g r19.21aivva df-iso ex w3a isof1o f1ofo syl
      3ad2ant1 smoiso jca 3expib com12 impbid ) AFZBGHZIZABCJZCKZIZABLLCMZVMVPV
      QVMVPIABCNZDOZEOZLPZVSCQZVTCQZLPZRZEASDASZVQVPVRVMVPABCTZVNVRVNABCUAVOWGA
      BCUBABCUCUDVNVOUEABCUFUGUHVPWFVMVNCAUIZVOWFABCUJWHVOIZWEDEAAWIVSAUKVTAUKI
      IDEULWBWCUKWAWDAVSVTCUMDEUNWBWCVSCUOVTCUOUPUQURUDUHDEABLLCUSUGUTVQVMVPVQV
      KVLVPVQVKVLVAVNVOVQVKVNVLVQVRVNABLLCVBABCVCVDVEABCVFVGVHVIVJ $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y B $.
    smo.1 $e |- dom A = B $.
    smo.2 $e |- Smo A $.
    $( A strictly monotonic function is always increasing.  Exercise 1 in
       [TakeutiZaring] p. 50.  It holds even when ` C ` is transfinite.
       (Contributed by Andrew Salmon, 23-Nov-2011.) $)
    smoge $p |- ( C e. B -> C C_ ( A ` C ) ) $=
      ( vx vy con0 wcel cfv wss cdm eleq2i word csmo smodm ax-mp ordelon mpan
      sylbir cv wi c0 csuc wceq eleq1 id fveq2 sseq12d imbi12d 0ss a1i wa wtr
      ordtr wb treq sylib trsuc visset sucid smoel mp3an13 adantl smofvon
      ontr2 ancomsd exp3a sylan2 mpd eloni ordelsuc sylibd ex syl5bir com23
      imim2d imp syl5 pm2.43d wral wlim ralim ordeq mpbi ordtr1 expcom
      r19.21aiv ciun ss2iun cuni limuni uniiun syl6req sseq1d smoiun sylan2br
      sstr syl com12 syl6bi imim12d pm2.43 syl6 tfinds mpcom ) CHIZCBIZCCAJZKZX
      HCALZIZXGXKBCDMXKNZXLXGAOZXMEAPQZXKCRSTFUAZBIZXPXPAJZKZUBZUCBIZUCUCAJZKZU
      BGUAZBIZYDYDAJZKZUBZYDUDZBIZYIYIAJZKZUBZXHXJUBFGCXPUCUEZXQYAXSYCXPUCBUFYN
      XPUCXRYBYNUGXPUCAUHUIUJXPYDUEZXQYEXSYGXPYDBUFYOXPYDXRYFYOUGXPYDAUHUIUJXPY
      IUEZXQYJXSYLXPYIBUFYPXPYIXRYKYPUGXPYIAUHUIUJXPCUEZXQXHXSXJXPCBUFYQXPCXRXI
      YQUGXPCAUHUIUJYCYAYBUKULYDHIZYHYMYRYHUMZYJYLYJYEYSYMBUNZYJYEXMYTXOXMXKUNZ
      YTXKUOXKBUEZUUAYTUPDXKBUQQURQBYDUSSYRYHYEYMUBYRYGYMYEYRYJYGYLYJYIXKIZYRYG
      YLUBZXKBYIDMYRUUCUUDYRUUCUMZYGYDYKIZYLUUEYFYKIZYGUUFUBZUUCUUGYRXNUUCYDYII
      UUGEYDGUTVAYIAYDVBVCVDUUCYRYKHIZUUGUUHUBXNUUCUUIEYIAVESZYRUUIUMZUUGYGUUFU
      UKYGUUGUUFYDYFYKVFVGVHVIVJUUCYRUUIUUFYLUPZUUJUUIYRYKNUULYKVKYDYKHVLVIVIVM
      VNVOVPVQVRVSVTVNYHGXPWAYEGXPWAZYGGXPWAZUBZXPWBZXTYEYGGXPWCUUPUUOXQXTUBXTU
      UPXQUUMUUNXTXQUUMUBUUPXQYEGXPYDXPIZXQYEBNZUUQXQUMYEUBXMUURXOUUBXMUURUPDXK
      BWDQWEYDXPBWFQWGWHULUUNGXPYDWIZGXPYFWIZKZUUPXTGXPYDYFWJUUPUVAXPUUTKZXTUUP
      UUSXPUUTUUPXPXPWKUUSXPWLGXPWMWNWOXQUVBXSXQUUTXRKZUVBXSUBXNXQUVCEXQXNXPXKI
      UVCXKBXPDMGXPAWPWQSUVBUVCXSXPUUTXRWRWGWSWTXAVSXBXQXSXCXDVSXEXF $.
      $( [23-Nov-2011] $)
  $}

