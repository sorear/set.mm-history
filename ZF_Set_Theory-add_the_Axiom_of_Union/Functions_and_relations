$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Functions and relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c X. $. $( Times symbol (cross product symbol) (read: 'cross') $)
  $c `' $. $( Small elevated smiley (converse operation) $)
  $c dom $. $( Domain $)
  $c ran $. $( Range $)
  $c |` $. $( Right hook (restriction symbol) $)
  $c " $. $( Left quote (image symbol) $)
  $c o. $. $( Small circle (composition symbol) $)
  $c Rel $. $( Relation predicate $)
  $c Fun $. $( Function predicate $)
  $c Fn $. $( Function connective $)
  $c : $. $( Colon $)
  $c --> $. $( Domain-codomain connective $)
  $c -1-1-> $. $( 'One-to-one' domain-codomain connective $)
  $c -onto-> $. $( 'Onto' domain-codomain connective $)
  $c -1-1-onto-> $. $( 'One-to-one' and 'onto' domain-codomain connective $)
  $c ` $. $( Left apostrophe (function value symbol) $)
  $c Isom $. $( Isomorphism $)

  $( Extend the definition of a class to include the cross product. $)
  cxp $a class ( A X. B ) $.

  $( Extend the definition of a class to include the converse of a class. $)
  ccnv $a class `' A $.

  $( Extend the definition of a class to include the domain of a class. $)
  cdm $a class dom A $.

  $( Extend the definition of a class to include the range of a class. $)
  crn $a class ran A $.

  $( Extend the definition of a class to include the restriction of a class.
     (Read:  The restriction of ` A ` to ` B ` .) $)
  cres $a class ( A |` B ) $.

  $( Extend the definition of a class to include the image of a class.  (Read:
     The image of ` B ` under ` A ` .) $)
  cima $a class ( A " B ) $.

  $( Extend the definition of a class to include the composition of two
     classes.  (Read:  The composition of ` A ` and ` B ` .) $)
  ccom $a class ( A o. B ) $.

  $( Extend the definition of a wff to include the relation predicate.  (Read:
     ` A ` is a relation.) $)
  wrel $a wff Rel A $.

  $( Extend the definition of a wff to include the function predicate.  (Read:
     ` A ` is a function.) $)
  wfun $a wff Fun A $.

  $( Extend the definition of a wff to include the function predicate with a
     domain.  (Read: ` A ` is a function on ` B ` .) $)
  wfn $a wff A Fn B $.

  $( Extend the definition of a wff to include the function predicate with
     domain and codomain.  (Read: ` F ` maps ` A ` into ` B ` .) $)
  wf $a wff F : A --> B $.

  $( Extend the definition of a wff to include one-to-one functions.  (Read:
     ` F ` maps ` A ` one-to-one into ` B ` .)  The notation ("1-1" above the
     arrow) is from Definition 6.15(5) of [TakeutiZaring] p. 27. $)
  wf1 $a wff F : A -1-1-> B $.

  $( Extend the definition of a wff to include onto functions.  (Read: ` F `
     maps ` A ` onto ` B ` .)  The notation ("onto" below the arrow) is from
     Definition 6.15(4) of [TakeutiZaring] p. 27. $)
  wfo $a wff F : A -onto-> B $.

  $( Extend the definition of a wff to include one-to-one onto functions.
     (Read: ` F ` maps ` A ` one-to-one onto ` B ` .)  The notation ("1-1"
     above the arrow and "onto" below the arrow) is from Definition 6.15(6) of
     [TakeutiZaring] p. 27. $)
  wf1o $a wff F : A -1-1-onto-> B $.

  $( Extend the definition of a class to include the value of a function.
     (Read:  The value of ` F ` at ` A ` , or " ` F ` of ` A ` .") $)
  cfv $a class ( F ` A ) $.

  $( Extend the definition of a wff to include the isomorphism property.
     (Read: ` H ` is an ` R ` , ` S ` isomorphism of ` A ` onto ` B ` .) $)
  wiso $a wff H Isom R , S ( A , B ) $.

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( Define the cross product of two classes.  Definition 9.11 of [Quine]
       p. 64. $)
    df-xp $a |- ( A X. B ) = { <. x , y >. | ( x e. A /\ y e. B ) } $.

    $( Define the relation predicate.  Definition 6.4(1) of [TakeutiZaring]
       p. 23.  For alternate definitions, see ~ dfrel2 and ~ dfrel3 . $)
    df-rel $a |- ( Rel A <-> A C_ ( _V X. _V ) ) $.

    $( Define the converse of a class.  Definition 9.12 of [Quine] p. 64.  We
       use Quine's breve accent (smile) notation.  Like Quine, we use it as a
       prefix, which eliminates the need for parentheses.  Many authors use the
       postfix superscript "to the minus one."  "Converse" is Quine's
       terminology; some authors call it "inverse," especially when the
       argument is a function. $)
    df-cnv $a |- `' A = { <. x , y >. | y A x } $.

    $( Define the composition of two classes.  Definition 6.6(3) of
       [TakeutiZaring] p. 24.  Note that Definition 7 of [Suppes] p. 63
       reverses ` A ` and ` B ` , uses ` /. ` instead of ` o. ` , and calls the
       operation "relative product." $)
    df-co $a |- ( A o. B ) = { <. x , y >. | E. z ( x B z /\ z A y ) } $.

    $( Define the domain of a class.  Definition 3 of [Suppes] p. 59.  For
       alternate definitions see ~ dfdm2 , ~ dfdm3 , and ~ dfdm4 .  The
       notation " ` dom ` " is used by Enderton; other authors sometimes use
       script D. $)
    df-dm $a |- dom A = { x | E. y x A y } $.

    $( Define the range of a class.  For an alternate definitions, see
       ~ dfrn2 , ~ dfrn3 , and ~ dfrn4 .  The notation " ` ran ` " is used by
       Enderton; other authors sometimes use script R or script W. $)
    df-rn $a |- ran A = dom `' A $.

    $( Define the restriction of a class.  Definition 6.6(1) of [TakeutiZaring]
       p. 24. $)
    df-res $a |- ( A |` B ) = ( A i^i ( B X. _V ) ) $.

    $( Define the image of a class.  Definition 6.6(2) of [TakeutiZaring]
       p. 24.  For an alternate definition, see ~ dfima2 . $)
    df-ima $a |- ( A " B ) = ran ( A |` B ) $.

    $( Define a function.  Definition 10.1 of [Quine] p. 65.  For alternate
       definitions, see ~ dffun2 , ~ dffun3 , ~ dffun4 , ~ dffun5 , ~ dffun6 ,
       ~ dffun7 , ~ dffun8 , and ~ dffun9 . $)
    df-fun $a |- ( Fun A <-> ( Rel A /\ ( A o. `' A ) C_ _I ) ) $.

    $( Define a function with domain.  Definition 6.15(1) of [TakeutiZaring]
       p. 27.  For alternate definitions, see ~ dffn2 , ~ dffn3 , ~ dffn4 , and
       ~ dffn5 . $)
    df-fn $a |- ( A Fn B <-> ( Fun A /\ dom A = B ) ) $.

    $( Define a function (mapping) with domain and codomain.  Definition
       6.15(3) of [TakeutiZaring] p. 27.  For alternate definitions, see
       ~ dff2 , ~ dff3 , and ~ dff4 . $)
    df-f $a |- ( F : A --> B <-> ( F Fn A /\ ran F C_ B ) ) $.

    $( Define a one-to-one function.  For equivalent definitions see ~ dff12
       and ~ dff13 .  Compare Definition 6.15(5) of [TakeutiZaring] p. 27.  We
       use their notation ("1-1" above the arrow). $)
    df-f1 $a |- ( F : A -1-1-> B <-> ( F : A --> B /\ Fun `' F ) ) $.

    $( Define an onto function.  Definition 6.15(4) of [TakeutiZaring] p. 27.
       We use their notation ("onto" under the arrow).  For alternate
       definitions, see ~ dffo2 , ~ dffo3 , ~ dffo4 , and ~ dffo5 . $)
    df-fo $a |- ( F : A -onto-> B <-> ( F Fn A /\ ran F = B ) ) $.

    $( Define a one-to-one onto function.  For equivalent definitions see
       ~ dff1o2 , ~ dff1o3 , ~ dff1o4 , and ~ dff1o5 .  Compare Definition
       6.15(6) of [TakeutiZaring] p. 27.  We use their notation ("1-1" above
       the arrow and "onto" below the arrow). $)
    df-f1o $a |- ( F : A -1-1-onto-> B <->
                ( F : A -1-1-> B /\ F : A -onto-> B ) ) $.

    $( Define the value of a function.  Although based on the idea embodied by
       Definition 10.2 of [Quine] p. 65 (see ~ args ), our definition
       apparently does not appear in the literature; but it is quite
       convenient: it can be applied to any class and evaluates to the empty
       set when it is not meaningful (as shown by ~ ndmfv and ~ fvprc ).  The
       left apostrophe notation originated with Peano and was adopted in
       Definition *30.01 of [WhiteheadRussell] p. 235, Definition 10.11 of
       [Quine] p. 68, and Definition 6.11 of [TakeutiZaring] p. 26.  It means
       the same thing as the more familiar ` F ( A ) ` notation for a
       function's value at ` A ` , i.e.  " ` F ` of ` A ` ," but without
       context-dependent notational ambiguity.  Alternate definitions are
       ~ dffv2 and ~ dffv3 .  For other alternate definitions (that fail to
       evaluate to the empty set for proper classes), see ~ fv2 , ~ fv3 , and
       ~ fv4 .  Restricted equivalents that require ` F ` to be a function are
       shown in ~ funfv and ~ funfv2 .  For the familiar definition of function
       value in terms of ordered pair membership, see ~ funopfvb . $)
    df-fv $a |- ( F ` A ) = U. { x | ( F " { A } ) = { x } } $.
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $( Define the isomorphism predicate.  We read this as " ` H ` is an ` R ` ,
       ` S ` isomorphism of ` A ` onto ` B ` ."  Normally, ` R ` and ` S ` are
       ordering relations on ` A ` and ` B ` respectively.  Definition 6.28 of
       [TakeutiZaring] p. 32, whose notation is the same as ours except that
       ` R ` and ` S ` are subscripts. $)
    df-iso $a |- ( H Isom R , S ( A , B ) <-> ( H : A -1-1-onto-> B /\
                 A. x e. A A. y e. A ( x R y <-> ( H ` x ) S ( H ` y ) ) ) ) $.

  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Equality theorem for cross product. $)
    xpeq1 $p |- ( A = B -> ( A X. C ) = ( B X. C ) ) $=
      ( vx vy wceq cv wcel wa copab cxp eleq2 anbi1d opabbidv df-xp 3eqtr4g ) A
      BFZDGZAHZEGCHZIZDEJRBHZTIZDEJACKBCKQUAUCDEQSUBTABRLMNDEACODEBCOP $.
      $( [4-Jul-1994] $)

    $( Equality theorem for cross product. $)
    xpeq2 $p |- ( A = B -> ( C X. A ) = ( C X. B ) ) $=
      ( vx vy wceq cv wcel wa copab cxp eleq2 anbi2d opabbidv df-xp 3eqtr4g ) A
      BFZDGCHZEGZAHZIZDEJRSBHZIZDEJCAKCBKQUAUCDEQTUBRABSLMNDECAODECBOP $.
      $( [5-Jul-1994] $)

    $( Membership in a cross product.  Uses fewer axioms than ~ elxp . $)
    elxpi $p |- ( A e. ( B X. C ) -> E. x E. y ( A = <. x , y >. /\
               ( x e. B /\ y e. C ) ) ) $=
      ( vz cv cop wceq wcel wa wex cab cxp eqeq1 anbi1d 2exbidv elabg ibi copab
      df-xp df-opab eqtri eleq2s ) CAGZBGZHZIZUEDJUFEJKZKZBLALZCFGZUGIZUIKZBLAL
      ZFMZDENZCUPJUKUOUKFCUPULCIZUNUJABURUMUHUIULCUGOPQRSUQUIABTUPABDEUAUIABFUB
      UCUD $.
      $( [4-Jul-1994] $)

    $( Membership in a cross product. $)
    elxp $p |- ( A e. ( B X. C ) <-> E. x E. y ( A = <. x , y >. /\
               ( x e. B /\ y e. C ) ) ) $=
      ( cxp wcel cv wa copab cop wceq wex df-xp eleq2i elopab bitri ) CDEFZGCAH
      ZDGBHZEGIZABJZGCSTKLUAIBMAMRUBCABDENOUAABCPQ $.
      $( [4-Jul-1994] $)

    $( Membership in a cross product. $)
    elxp2 $p |- ( A e. ( B X. C ) <-> E. x e. B E. y e. C A = <. x , y >. ) $=
      ( cv wcel cop wceq wrex wex cxp df-rex r19.42v anass ancom 3bitr4ri exbii
      wa an12 3bitr3i elxp ) AFZDGZCUCBFZHIZBEJZSZAKUFUDUEEGZSZSZBKZAKUGADJCDEL
      GUHULAUDUFSZBEJUIUMSZBKUHULUMBEMUDUFBENUNUKBUJUFSUDUIUFSSUKUNUDUIUFOUFUJP
      UIUDUFTQRUARUGADMABCDEUBQ $.
      $( [23-Feb-2004] $)
  $}

  $( Equality theorem for cross product.  (Contributed by FL, 31-Aug-2009.) $)
  xpeq12 $p |- ( ( A = B /\ C = D ) -> ( A X. C ) = ( B X. D ) ) $=
    ( wceq cxp xpeq1 xpeq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [31-Aug-2009] $)

  ${
    xpeq1i.1 $e |- A = B $.
    $( Equality inference for cross product. $)
    xpeq1i $p |- ( A X. C ) = ( B X. C ) $=
      ( wceq cxp xpeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [21-Dec-2008] $)

    $( Equality inference for cross product. $)
    xpeq2i $p |- ( C X. A ) = ( C X. B ) $=
      ( wceq cxp xpeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [21-Dec-2008] $)
  $}

  ${
    xpeq12i.1 $e |- A = B $.
    xpeq12i.2 $e |- C = D $.
    $( Equality inference for cross product.  (Contributed by FL,
       31-Aug-2009.) $)
    xpeq12i $p |- ( A X. C ) = ( B X. D ) $=
      ( wceq cxp xpeq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
      $( [31-Aug-2009] $)
  $}

  ${
    xpeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for cross product.  (Contributed by Jeff Madsen,
       17-Jun-2010.) $)
    xpeq1d $p |- ( ph -> ( A X. C ) = ( B X. C ) ) $=
      ( wceq cxp xpeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [17-Jun-2010] $)

    $( Equality deduction for cross product.  (Contributed by Jeff Madsen,
       17-Jun-2010.) $)
    xpeq2d $p |- ( ph -> ( C X. A ) = ( C X. B ) ) $=
      ( wceq cxp xpeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [17-Jun-2010] $)

    xpeq12d.2 $e |- ( ph -> C = D ) $.
    $( Equality deduction for cross product. $)
    xpeq12d $p |- ( ph -> ( A X. C ) = ( B X. D ) ) $=
      ( wceq cxp xpeq12 syl2anc ) ABCHDEHBDICEIHFGBCDEJK $.
      $( [8-Dec-2013] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d x y z w $.
    hbxp.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbxp.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for cross product.  (The proof was
       shortened by Andrew Salmon, 12-Aug-2011.) $)
    hbxp $p |- ( y e. ( A X. B ) -> A. x y e. ( A X. B ) ) $=
      ( vz vw cxp cv wcel wa copab df-xp hblem hban hbopab hbxfreq ) ABCDIGJCKZ
      HJDKZLZGHMGHCDNUAGHABSTAABGCEOABHDFOPQR $.
      $( [12-Aug-2011] $) $( [15-Sep-2003] $)
  $}

  ${
    $d A w y z $.  $d B w y z $.  $d C w y z $.  $d D w y z $.  $d w x y z $.
    $( Distribute proper substitution through the cross product of two classes.
       ~ csbxpg is derived from the virtual deduction proof ~ csbxpgVD .
       (Contributed by Alan Sare, 10-Nov-2012. ) $)
    csbxpg $p |- ( A e. D -> [_ A / x ]_ ( B X. C ) =
                ( [_ A / x ]_ B X. [_ A / x ]_ C ) ) $=
      ( vz vw vy wcel cxp csb cv wa wex cab wsbc copab df-xp df-opab bitrd wceq
      cop eqtri csbeq2i csbabg sbcexg sbcang sbcg sbcel2g anbi12d exbidv abbidv
      3eqtrd syl6eqr ) BEIZABCDJZKZFLGLZHLZUBUAZURABCKZIZUSABDKZIZMZMZHNZGNZFOZ
      VAVCJZUOUQABUTURCIZUSDIZMZMZHNZGNZFOZKVPABPZFOVIABUPVQEUPVMGHQVQGHCDRVMGH
      FSUCUDVPAFBEUEUOVRVHFUOVRVOABPZGNVHVOGABEUFUOVSVGGUOVSVNABPZHNVGVNHABEUFU
      OVTVFHUOVTUTABPZVMABPZMVFUTVMABEUGUOWAUTWBVEUTABEUHUOWBVKABPZVLABPZMVEVKV
      LABEUGUOWCVBWDVDABURCEUIABUSDEUIUJTUJTUKTUKTULUMVJVEGHQVIGHVAVCRVEGHFSUCU
      N $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
    $( The first member of an ordered pair of classes in a cross product
       belongs to first cross product argument. $)
    opelxp1 $p |- ( <. A , B >. e. ( C X. D ) -> A e. C ) $=
      ( vx vy cop cxp wcel cv wceq wa wex elxp vex eqcoms eleq1d biimpa adantrr
      opth1 exlimivv sylbi ) ABGZCDHIUCEJZFJZGZKZUDCIZUEDIZLLZFMEMACIZEFUCCDNUJ
      UKEFUGUHUKUIUGUHUKUGUDACUDAKUFUCUDUEABEOTPQRSUAUB $.
      $( [28-May-2008] $)
  $}

  $( The first member of an ordered triple of classes in a cross product
     belongs to first cross product argument. $)
  otelxp1 $p |- ( <. <. A , B >. , C >. e. ( ( R X. S ) X. T )
          -> A e. R ) $=
    ( cop cxp wcel opelxp1 syl ) ABGZCGDEHZFHILMIADILCMFJABDEJK $.
    $( [28-May-2008] $)

  ${
    $d x y z A $.  $d x y z B $.  $d y z ph $.  $d x ps $.
    rabxp.1 $e |- ( x = <. y , z >. -> ( ph <-> ps ) ) $.
    $( Membership in a class builder restricted to a cross product. $)
    rabxp $p |- { x e. ( A X. B ) | ph }
             = { <. y , z >. | ( y e. A /\ z e. B /\ ps ) } $=
      ( cv cxp wcel wa cab cop wceq w3a wex crab copab elxp anbi1i anass anbi2d
      19.41vv df-3an syl6bbr pm5.32i bitri 2exbii 3bitr2i abbii df-opab 3eqtr4i
      df-rab ) CIZFGJZKZALZCMUODIZEIZNOZUSFKZUTGKZBPZLZEQDQZCMACUPRVDDESURVFCUR
      VAVBVCLZLZEQDQZALVHALZEQDQVFUQVIADEUOFGTUAVHADEUDVJVEDEVJVAVGALZLVEVAVGAU
      BVAVKVDVAVKVGBLVDVAABVGHUCVBVCBUEUFUGUHUIUJUKACUPUNVDDECULUM $.
      $( [20-Feb-2014] $)
  $}

  $( A true binary relation on a relation implies the first argument is a set.
     (This is a property of our ordered pair definition.) $)
  brrelex $p |- ( ( Rel R /\ A R B ) -> A e. _V ) $=
    ( wrel wbr cvv wcel cxp wss df-rel cop df-br ssel syl5bi opelxp1 syl6 sylbi
    wi imp ) CDZABCEZAFGZTCFFHZIZUAUBRCJUDUAABKZUCGZUBUAUECGUDUFABCLCUCUEMNABFF
    OPQS $.
    $( [18-May-2004] $)

  ${
    brrelexi.1 $e |- Rel R $.
    $( The first argument of a binary relation exists.  (An artifact of our
       ordered pair definition.) $)
    brrelexi $p |- ( A R B -> A e. _V ) $=
      ( wrel wbr cvv wcel brrelex mpan ) CEABCFAGHDABCIJ $.
      $( [4-Jun-1998] $)
  $}

  ${
    nprrel.1 $e |- Rel R $.
    nprrel.2 $e |- -. A e. _V $.
    $( No proper class is related to anything via any relation.  (Contributed
       by Roy F. Longton, 30-Jul-2005.) $)
    nprrel $p |- -. A R B $=
      ( wbr cvv wcel brrelexi mto ) ABCFAGHEABCDIJ $.
      $( [30-Jul-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Representation of a constant function using ordered pairs. $)
    fconstopab $p |- ( A X. { B } ) = { <. x , y >. | ( x e. A /\ y = B ) } $=
      ( csn cxp cv wcel wa copab wceq df-xp elsn anbi2i opabbii eqtri ) CDEZFAG
      CHZBGZQHZIZABJRSDKZIZABJABCQLUAUCABTUBRBDMNOP $.
      $( [12-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y z C $.  $d x y z R $.
    vtoclr.1 $e |- Rel R $.
    vtoclr.2 $e |- ( ( x R y /\ y R z ) -> x R z ) $.
    $( Variable to class conversion of transitive relation. $)
    vtoclr $p |- ( C e. D -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( wcel cvv wbr wa wi brrelexi cv wceq breq1 breq2 imbi12d anbi12d vtocl2g
      elex anbi1d imbi2d imbi1d anbi2d vtoclg syl2an pm2.43b syl ) FGKFLKZDEHMZ
      EFHMZNZDFHMZOZFGUDUMUPUQUNDLKELKUMUROZUODEHIPEFHIPUMAQZBQZHMZVAFHMZNZUTFH
      MZOZOUMDVAHMZVCNZUQOZOUSABDELLUTDRZVFVIUMVJVDVHVEUQVJVBVGVCUTDVAHSUEUTDFH
      SUAUFVAERZVIURUMVKVHUPUQVKVGUNVCUOVAEDHTVAEFHSUBUGUFVBVACQZHMZNZUTVLHMZOV
      FCFLVLFRZVNVDVOVEVPVMVCVBVLFVAHTUHVLFUTHTUAJUIUCUJUKUL $.
      $( [9-Jun-1998] $)

    ${
      vtoclrbr.3 $e |- x R x $.
      $( Variable to class conversion of transitive, reflexive relation.  (The
         proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
      vtoclrbr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( cvv wcel wbr wa wi vtoclr wn brrelexi cv wceq id breq12d vtoclg brprc
        syl5ibr syl5 adantrd pm2.61i ) FKLZDEGMZEFGMZNDFGMZOABCDEFKGHIPUIQZUJUL
        UKUJDKLZUMULDEGHRUNULUMDDGMZASZUPGMUOADKUPDTZUPDUPDGUQUAZURUBJUCDFGUDUE
        UFUGUH $.
        $( [27-Aug-2011] $) $( [9-Jun-1998] $)
    $}

    ${
      $d x B $.
      vtoclibr.3 $e |- -. x R x $.
      $( Variable to class conversion of transitive, irreflexive relation. $)
      vtoclibr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( wbr cvv wcel wa wi wn cv wceq breq1 breq2 bitrd notbid brrelexi con3i
        vtoclg pm2.61i brprc mtbiri con4i vtoclr syl anabsi7 ) DEGKZEFGKZDFGKZU
        NFLMZUMUNNUOOUPUNUPPUNEEGKZELMZUQPZAQZUTGKZPUSAELUTERZVAUQVBVAEUTGKUQUT
        EUTGSUTEEGTUAUBJUEUQUREEGHUCUDUFEFGUGUHUIABCDEFLGHIUJUKUL $.
        $( [9-Jun-1998] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    opelxp.1 $e |- B e. _V $.
    $( Ordered pair membership in a cross product.  (The proof was shortened by
       Andrew Salmon, 12-Aug-2011.) $)
    opelxp $p |- ( <. A , B >. e. ( C X. D ) <-> ( A e. C /\ B e. D ) ) $=
      ( vx vy cop cxp wcel wa opelxp1 simpl cv wceq wex elxp cvv wb eleq1 mpan2
      bi2anan9 copsex2g syl5bb pm5.21nii ) ABHZCDIJZACJZUHBDJZKZABCDLUHUIMUGUFF
      NZGNZHOUKCJZULDJZKZKGPFPZUHUJFGUFCDQUHBRJUPUJSEUOUJFGABCRUKAOUMUHULBOUNUI
      UKACTULBDTUBUCUAUDUE $.
      $( [12-Aug-2011] $) $( [15-Nov-1994] $)

    $( Binary relation on a cross product. $)
    brxp $p |- ( A ( C X. D ) B <-> ( A e. C /\ B e. D ) ) $=
      ( cxp wbr cop wcel wa df-br opelxp bitri ) ABCDFZGABHNIACIBDIJABNKABCDELM
      $.
      $( [22-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( Ordered pair membership in a cross product. $)
    opelxpg $p |- ( B e. V ->
                    ( <. A , B >. e. ( C X. D ) <-> ( A e. C /\ B e. D ) ) ) $=
      ( vx cv cop cxp wcel wa wceq opeq2 eleq1d eleq1 anbi2d vex opelxp vtoclbg
      ) AFGZHZCDIZJACJZTDJZKABHZUBJUCBDJZKFBETBLZUAUEUBTBAMNUGUDUFUCTBDOPATCDFQ
      RS $.
      $( [28-May-1995] $)
  $}

  $( Ordered pair membership in a cross product (implication). $)
  opelxpi $p |- ( ( A e. C /\ B e. D ) -> <. A , B >. e. ( C X. D ) ) $=
    ( wcel cop cxp wa opelxpg biimprd anabsi7 ) ACEZBDEZABFCDGEZMNLMHABCDDIJK
    $.
    $( [28-May-1995] $)

  $( Ordered pair membership in a semi-universal class of ordered pairs. $)
  opelxpv $p |- ( A e. C <-> <. A , B >. e. ( C X. _V ) ) $=
    ( wcel cop cvv opelxpi wn wa wceq opprc2 adantl opelxpg id mpbir2and adantr
    cxp elex eqeltrd pm2.61dan opelxp1 impbii ) ACDZABEZCFQZDZUCBFDZUFABCFGUCUG
    HZIUDAAEZUEUHUDUIJUCABKLUCUIUEDZUHUCUJUCAFDAACFCMUCNACROPSTABCFUAUB $.
    $( [1-Oct-2013] $)

  ${
    opelvv.1 $e |- A e. _V $.
    $( Ordered pair membership in the universal class of ordered pairs. $)
    opelvv $p |- <. A , B >. e. ( _V X. _V ) $=
      ( cvv wcel cop cxp opelxpv mpbi ) ADEABFDDGECABDHI $.
      $( [22-Aug-2013] $)
  $}

  $( A property of an ordered pair of proper classes (due to our particular
     definition of ordered pair). $)
  opprc4b $p |- ( -. A e. _V <-> -. <. A , B >. e. ( _V X. _V ) ) $=
    ( cvv wcel cop cxp opelxpv notbii ) ACDABECCFDABCGH $.
    $( [1-Oct-2013] $)

  ${
    $d x y z A $.  $d x y z B $.  $d y z ph $.  $d x ps $.
    ralxp.1 $e |- ( x = <. y , z >. -> ( ph <-> ps ) ) $.
    $( Universal quantification restricted to a cross product is equivalent to
       a double restricted quantification.  The hypothesis specifies an
       implicit substitution. $)
    ralxp $p |- ( A. x e. ( A X. B ) ph <-> A. y e. A A. z e. B ps ) $=
      ( cxp wral cv wcel wa cop wex wi hbra1 ax-17 hbim exlimi opelxp ralrimivv
      vex rcla4cv syl5bir wceq elxp simpl 2eximi sylbi hbral ra42 eleq1 imbi12d
      syl6bb syl5ibr syl pm2.43b ralrimiv impbii ) ACFGIZJZBEGJZDFJZVBBDEFGDKZF
      LZEKZGLMZVEVGNZVALZVBBVEVGFGEUCUAZABCVIVAHUDUEUBVDACVAVDCKZVALZAVMVLVIUFZ
      EOZDOZVDVMAPZPZVMVNVHMZEODOVPDEVLFGUGVSVNDEVNVHUHUIUJVOVRDVDVQDVCDFQVQDRS
      VNVREVDVQEVCEDFVFERBEGQUKVQERSVDVQVNVHBPBDEFGULVNVMVHABVNVMVJVHVLVIVAUMVK
      UOHUNUPTTUQURUSUT $.
      $( [7-Feb-2004] $)

    $( Existential quantification restricted to a cross product is equivalent
       to a double restricted quantification. $)
    rexxp $p |- ( E. x e. ( A X. B ) ph <-> E. y e. A E. z e. B ps ) $=
      ( wn cxp wral wrex cv cop wceq notbid ralxp ralnex ralbii dfrex2 3bitr4i
      bitri notbii ) AIZCFGJZKZIBEGLZIZDFKZIACUELUGDFLUFUIUFBIZEGKZDFKUIUDUJCDE
      FGCMDMEMNOABHPQUKUHDFBEGRSUBUCACUETUGDFTUA $.
      $( [14-Sep-2006] $) $( [11-Nov-1995] $)
  $}

  ${
    $d u v w x y A $.  $d u v w x y z B $.  $d u v w ph $.  $d u v w ps $.
    ralxpf.1 $e |- ( ph -> A. y ph ) $.
    ralxpf.2 $e |- ( ph -> A. z ph ) $.
    ralxpf.3 $e |- ( ps -> A. x ps ) $.
    ralxpf.4 $e |- ( x = <. y , z >. -> ( ph <-> ps ) ) $.
    $( Version of ~ ralxp with bound-variable hypotheses. $)
    ralxpf $p |- ( A. x e. ( A X. B ) ph <-> A. y e. A A. z e. B ps ) $=
      ( vw vu vv wral cv ax-17 wcel wceq vex wsbc cbvralsv ralbii hbral sbequ12
      cxp hbs1 ralbidv cbvral cop wa wex wb eqvinop cvv wal hbsbcg ax-mp sbhypf
      wi hbbi opth sylan9bb sylbi exlimi ralxp 3bitr4ri bitri ) ACFGUFZOACLPZUA
      ZLVIOZBEGOZDFOZACLVIUBBDMPZUAZEGOZMFOVPENPZUAZNGOZMFOVNVLVQVTMFVPENGUBUCV
      MVQDMFVMMQVPDEGEPZGRDQBDMUGZUDDPZVOSZBVPEGBDMUEZUHUIVKVSLMNFGVJVOVRUJZSVJ
      WCWAUJZSZWGWFSZUKZEULZDULVKVSUMZDEVJVOVRMTNTZUNWKWLDVKVSDVJUORZVKVKDUPUTL
      TZADCCVJUOCPZVJRZDQHUQURVRUORVSVSDUPUTWMVPDECVRUOWPVRRDQWBUQURVAWJWLEVKVS
      EWNVKVKEUPUTWOAECCVJUOWQEQIUQURVPENUGVAWHVKBWIVSABCLWGJKUSWIWDWAVRSZUKBVS
      UMWCWAVOVRDTETWMVBWDBVPWRVSWEVPENUEVCVDVCVEVEVDVFVGVH $.
      $( [20-Dec-2008] $) $( [18-Aug-2006] $)

    $( Version of ~ rexxp with bound-variable hypotheses. $)
    rexxpf $p |- ( E. x e. ( A X. B ) ph <-> E. y e. A E. z e. B ps ) $=
      ( wn cxp wral wrex hbn cv cop wceq dfrex2 notbid ralxpf ralnex 3bitr4i
      ralbii bitri notbii ) ALZCFGMZNZLBEGOZLZDFNZLACUIOUKDFOUJUMUJBLZEGNZDFNUM
      UHUNCDEFGADHPAEIPBCJPCQDQEQRSABKUAUBUOULDFBEGUCUEUFUGACUITUKDFTUD $.
      $( [19-Dec-2008] $)
  $}

  ${
    $d w x y A $.  $d w x y z B $.  $d w C $.  $d w D $.
    iunxpf.1 $e |- ( w e. C -> A. y w e. C ) $.
    iunxpf.2 $e |- ( w e. C -> A. z w e. C ) $.
    iunxpf.3 $e |- ( w e. D -> A. x w e. D ) $.
    iunxpf.4 $e |- ( x = <. y , z >. -> C = D ) $.
    $( Indexed union on a cross product is equals a double indexed union.  The
       hypothesis specifies an implicit substitution. $)
    iunxpf $p |- U_ x e. ( A X. B ) C = U_ y e. A U_ z e. B D $=
      ( cxp ciun cv wcel wrex cop wceq eliun eleq2d rexxpf rexbii bitri 3bitr4i
      eqriv ) DAEFMZGNZBECFHNZNZDOZGPZAUGQUKHPZCFQZBEQZUKUHPUKUJPZULUMABCEFIJKA
      OBOCORSGHUKLUAUBAUKUGGTUPUKUIPZBEQUOBUKEUITUQUNBECUKFHTUCUDUEUF $.
      $( [19-Dec-2008] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( Justification theorem for an ordered pair definition that works for any
       classes, including proper classes.  This is a possible definition
       implied by the footnote in [Jech] p. 78, which says, "The sophisticated
       reader will not object to our use of a pair of classes." $)
    opthprc $p |- ( ( ( A X. { (/) } ) u. ( B X. { { (/) } } ) ) =
                    ( ( C X. { (/) } ) u. ( D X. { { (/) } } ) )
                  <-> ( A = C /\ B = D ) ) $=
      ( vx c0 cxp wceq wa wo 0ex opelxp mpbiran2 bianfi bitr4i orbi12i 3bitr4ri
      wcel elun p0ex csn cun cv cop eleq2 snid 0nep0 elsnc nemtbir biorfi eqrdv
      3bitr4g eqcom bitri wn wb biorf ax-mp jca xpeq1 uneq12 syl2an impbii ) AF
      UAZGZBVDUAZGZUBZCVDGZDVFGZUBZHZACHZBDHZIVLVMVNVLEACVLEUCZFUDZVHRZVPVKRZVO
      ARZVOCRZVHVKVPUEVPVERZVPVGRZJVSFVFRZJVQVSWAVSWBWCWAVSFVDRZVOFAVDKLFKUFZMW
      BVOBRZWCIWCVOFBVFKLWCWFWCFVDUGFVDKUHUIZNOPVPVEVGSWCVSWGUJQVPVIRZVPVJRZJVT
      WCJVRVTWHVTWIWCWHVTWDVOFCVDKLWEMWIVODRZWCIWCVOFDVFKLWCWJWGNOPVPVIVJSWCVTW
      GUJQULUKVLEBDVLVOVDUDZVHRZWKVKRZWFWJVHVKWKUEWKVERZWKVGRZJVDVDRZWFJZWLWFWN
      WPWOWFWNVSWPIWPVOVDAVDTLWPVSWPFVDUGWPVDFHFVDHVDFTUHVDFUMUNUIZNOWOWFVDVFRZ
      VOVDBVFTLVDTUFZMPWKVEVGSWPUOZWFWQUPWRWPWFUQURQWKVIRZWKVJRZJWPWJJZWMWJXBWP
      XCWJXBVTWPIWPVOVDCVDTLWPVTWRNOXCWJWSVOVDDVFTLWTMPWKVIVJSXAWJXDUPWRWPWJUQU
      RQULUKUSVMVEVIHVGVJHVLVNACVDUTBDVFUTVEVIVGVJVAVBVC $.
      $( [28-Sep-2003] $)
  $}

  ${
    brelg.1 $e |- R C_ ( C X. D ) $.
    $( Two things in a binary relation belong to the relation's domain. $)
    brelg $p |- ( B e. V -> ( A R B -> ( A e. C /\ B e. D ) ) ) $=
      ( wbr cop wcel wa df-br cxp sseli opelxpg syl5ib syl5bi ) ABEHABIZEJZBFJZ
      ACJBDJKZABELSRCDMZJTUAEUBRGNABCDFOPQ $.
      $( [17-May-1996] $)
  $}

  ${
    brel.1 $e |- B e. _V $.
    brel.2 $e |- R C_ ( C X. D ) $.
    $( Membership in superset of binary relation. $)
    brel $p |- ( A R B -> ( A e. C /\ B e. D ) ) $=
      ( cvv wcel wbr wa wi brelg ax-mp ) BHIABEJACIBDIKLFABCDEHGMN $.
      $( [16-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in a cross product. $)
    elxp3 $p |- ( A e. ( B X. C ) <->
            E. x E. y ( <. x , y >. = A /\ <. x , y >. e. ( B X. C ) ) ) $=
      ( cxp wcel cv cop wceq wa wex elxp eqcom vex opelxp anbi12i 2exbii bitr4i
      ) CDEFZGCAHZBHZIZJZUADGUBEGKZKZBLALUCCJZUCTGZKZBLALABCDEMUIUFABUGUDUHUEUC
      CNUAUBDEBOPQRS $.
      $( [5-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Distributive law for cross product over union.  Theorem 103 of [Suppes]
       p. 52. $)
    xpundi $p |- ( A X. ( B u. C ) ) = ( ( A X. B ) u. ( A X. C ) ) $=
      ( vx vy cv wcel cun wa copab cxp wo elun anbi2i andi bitri opabbii unopab
      eqtr4i df-xp uneq12i 3eqtr4i ) DFAGZEFZBCHZGZIZDEJZUCUDBGZIZDEJZUCUDCGZIZ
      DEJZHZAUEKABKZACKZHUHUJUMLZDEJUOUGURDEUGUCUIULLZIURUFUSUCUDBCMNUCUIULOPQU
      JUMDERSDEAUETUPUKUQUNDEABTDEACTUAUB $.
      $( [12-Aug-2004] $)

    $( Distributive law for cross product over union.  Similar to Theorem 103
       of [Suppes] p. 52. $)
    xpundir $p |- ( ( A u. B ) X. C ) = ( ( A X. C ) u. ( B X. C ) ) $=
      ( vx vy cv cun wcel wa copab cxp wo elun anbi1i andir bitri unopab eqtr4i
      opabbii df-xp uneq12i 3eqtr4i ) DFZABGZHZEFCHZIZDEJZUCAHZUFIZDEJZUCBHZUFI
      ZDEJZGZUDCKACKZBCKZGUHUJUMLZDEJUOUGURDEUGUIULLZUFIURUEUSUFUCABMNUIULUFOPS
      UJUMDEQRDEUDCTUPUKUQUNDEACTDEBCTUAUB $.
      $( [30-Sep-2002] $)
  $}

  ${
    $d w y z A $.  $d w y z B $.  $d w x y z C $.
    $( Distributive law for cross product over indexed union.  (Revised by
       Mario Carneiro, 27-Apr-2014.) $)
    xpiundi $p |- ( C X. U_ x e. A B ) = U_ x e. A ( C X. B ) $=
      ( vz vw vy ciun cxp cv wrex wcel wa wex eliun exbii df-rex rexbii 3bitr4i
      elxp2 cop wceq rexcom anbi1i rexcom4 r19.41v 3bitri eqriv ) EDABCHZIZABDC
      IZHZEJZFJGJZUAUBZGUIKZFDKZUMUKLZABKZUMUJLUMULLUOGCKZABKZFDKUTFDKZABKUQUSU
      TFADBUCUPVAFDUNUILZUOMZGNUNCLZABKZUOMZGNZUPVAVDVGGVCVFUOAUNBCOUDPUOGUIQVA
      VEUOMZGNZABKVIABKZGNVHUTVJABUOGCQRVIAGBUEVKVGGVEUOABUFPUGSRURVBABFGUMDCTR
      SFGUMDUITAUMBUKOSUH $.
      $( [27-Apr-2014] $)

    $( Distributive law for cross product over indexed union.  (Revised by
       Mario Carneiro, 27-Apr-2014.) $)
    xpiundir $p |- ( U_ x e. A B X. C ) = U_ x e. A ( B X. C ) $=
      ( vz vy vw ciun cxp cv cop wrex wcel wa df-rex rexbii eliun elxp2 3bitr4i
      wex wceq rexcom4 anbi1i r19.41v bitr4i exbii 3bitr4ri eqriv ) EABCHZDIZAB
      CDIZHZEJZFJZGJKUAGDLZFUILZUMUKMZABLZUMUJMUMULMUNUIMZUONZFTZUOFCLZABLZUPUR
      UNCMZUONZFTZABLVEABLZFTVCVAVEAFBUBVBVFABUOFCOPUTVGFUTVDABLZUONVGUSVHUOAUN
      BCQUCVDUOABUDUEUFUGUOFUIOUQVBABFGUMCDRPSFGUMUIDRAUMBUKQSUH $.
      $( [27-Apr-2014] $)
  $}

  $( The cross product of two unions. $)
  xpun $p |- ( ( A u. B ) X. ( C u. D ) ) =
            ( ( ( A X. C ) u. ( A X. D ) ) u. ( ( B X. C ) u. ( B X. D ) ) ) $=
    ( cun cxp xpundi xpundir uneq12i un4 3eqtri ) ABEZCDEFLCFZLDFZEACFZBCFZEZAD
    FZBDFZEZEOREPSEELCDGMQNTABCHABDHIOPRSJK $.
    $( [12-Aug-2004] $)

  ${
    $d w x y z A $.
    $( Membership in universal class of ordered pairs. $)
    elvv $p |- ( A e. ( _V X. _V ) <-> E. x E. y A = <. x , y >. ) $=
      ( cvv cxp wcel cv cop wceq wa wex elxp vex pm3.2i biantru 2exbii bitr4i )
      CDDEFCAGZBGZHIZRDFZSDFZJZJZBKAKTBKAKABCDDLTUDABUCTUAUBAMBMNOPQ $.
      $( [4-Jul-1994] $)

    $( Membership in universal class of ordered triples. $)
    elvvv $p |- ( A e. ( ( _V X. _V ) X. _V )
                 <-> E. x E. y E. z A = <. <. x , y >. , z >. ) $=
      ( vw cvv cxp wcel cv cop wceq wa wex elxp anass elvv anbi2i 2exbii bitr3i
      vex biantru ancom 19.42vv bitr2i 3bitr3i exrot4 excom opex eqeq2d ceqsexv
      opeq1 exbii bitri 3bitri ) DFFGZFGHDEIZCIZJZKZUPUOHZUQFHZLLZCMEMUPAIZBIZJ
      ZKZUSLZBMAMZCMEMZDVEUQJZKZCMZBMAMZECDUOFNVBVHECVBUSUTLZVALZVHUSUTVAOVNUSV
      FBMAMZLZVOVHUTVPUSABUPPQVAVNCTUAVHUSVFLZBMAMVQVGVRABVFUSUBRUSVFABUCUDUESR
      VIVGCMEMZBMAMVMVGABECUFVSVLABVSVGEMZCMVLVGECUGVTVKCUSVKEVEVCVDUHVFURVJDUP
      VEUQUKUIUJULUMRSUN $.
      $( [17-Dec-2008] $)

    $( An ordered pair contains its union. $)
    elvvuni $p |- ( A e. ( _V X. _V ) -> U. A e. A ) $=
      ( vx vy cvv cxp wcel cv cop wceq wex cuni elvv cpr uniop eqeltri unieq id
      opi2 eleq12d mpbiri exlimivv sylbi ) ADDEFABGZCGZHZIZCJBJAKZAFZBCALUFUHBC
      UFUHUEKZUEFUIUCUDMUEUCUDNUCUDROUFUGUIAUEAUEPUFQSTUAUB $.
      $( [16-Sep-2006] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( A cross product is included in the ordered pair universe.  Exercise 3 of
       [TakeutiZaring] p. 25. $)
    xpssOLD $p |- ( A X. B ) C_ ( _V X. _V ) $=
      ( vz vx vy cxp cvv cv cop wceq wcel wa wex simpl 2eximi elxp elvv 3imtr4i
      ssriv ) CABFZGGFZCHZDHZEHZIJZUCAKUDBKLZLZEMDMUEEMDMUBTKUBUAKUGUEDEUEUFNOD
      EUBABPDEUBQRS $.
      $( [2-Aug-1994] $)
  $}

  $( Intersection of binary relation with cross product. $)
  brinxp2 $p |- ( B e. S -> ( A ( R i^i ( C X. D ) ) B <->
                ( A e. C /\ B e. D /\ A R B ) ) ) $=
    ( wcel wbr cop cxp cin w3a opelxpg anbi2d elin df-br anbi1i 3bitr4i 3anrot
    wa 3anass bitr3i 3bitr4g ) BFGZABEHZABIZCDJZGZTZUEACGZBDGZTZTZABEUGKZHZUJUK
    UELZUDUHULUEABCDFMNUFUNGUFEGZUHTUOUIUFEUGOABUNPUEUQUHABEPQRUPUEUJUKLUMUEUJU
    KSUEUJUKUAUBUC $.
    $( [3-Mar-2007] $)

  $( Intersection of binary relation with cross product. $)
  brinxp $p |- ( ( A e. C /\ B e. D ) ->
               ( A R B <-> A ( R i^i ( C X. D ) ) B ) ) $=
    ( wcel wa wbr w3a cxp cin df-3an baibr wb brinxp2 adantl bitr4d ) ACFZBDFZG
    ZABEHZRSUAIZABECDJKHZUBTUARSUALMSUCUBNRABCDEDOPQ $.
    $( [9-Mar-1997] $)

  ${
    $d x y z A $.  $d x y z R $.
    $( Intersection of well-ordering with cross product of its field. $)
    weinxp $p |- ( R We A <-> ( R i^i ( A X. A ) ) We A ) $=
      ( vx vy vz wfr cv wbr w3o wral wa wwe wn wrex wi wal wb wcel ssel 3bitr4i
      wceq cxp cin wss c0 wne anim12d brinxp ancoms exp3a imp31 notbid ralbidva
      syl6 adantr pm5.74i albii df-fr biidd 3orbi123d 2albii r2al anbi12i dfwe2
      rexbidva ) ABFZCGZDGZBHZVGVHUAZVHVGBHZIZDAJCAJZKABAAUBUCZFZVGVHVNHZVJVHVG
      VNHZIZDAJCAJZKABLAVNLVFVOVMVSEGZAUDZVTUEUFZKZVKMZDVTJZCVTNZOZEPWCVQMZDVTJ
      ZCVTNZOZEPVFVOWGWKEWCWFWJWAWFWJQWBWAWEWICVTWAVGVTRZKZWDWHDVTWMVHVTRZKVKVQ
      WAWLWNVKVQQZWAWLWNWOWAWLWNKVGARZVHARZKZWOWAWLWPWNWQVTAVGSVTAVHSUGWQWPWOVH
      VGAABUHUIZUNUJUKULUMVEUOUPUQECDABURECDAVNURTWRVLOZDPCPWRVROZDPCPVMVSWTXAC
      DWRVLVRWRVIVPVJVJVKVQVGVHAABUHWRVJUSWSUTUPVAVLCDAAVBVRCDAAVBTVCCDABVDCDAV
      NVDT $.
      $( [9-Mar-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( An abstraction relation is a subset of a related cross product. $)
    opabssxp $p |- { <. x , y >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                   C_ ( A X. B ) $=
      ( cv wcel wa copab cxp simpl ssopab2i df-xp sseqtr4i ) BFDGCFEGHZAHZBCIOB
      CIDEJPOBCOAKLBCDEMN $.
      $( [16-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ps $.
    optocl.1 $e |- D = ( B X. C ) $.
    optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    optocl.3 $e |- ( ( x e. B /\ y e. C ) -> ph ) $.
    $( Implicit substitution of class for ordered pair. $)
    optocl $p |- ( A e. D -> ps ) $=
      ( wcel cxp eleq2i cv cop wceq wa wex sylbi vex opelxp syl5ib imp exlimivv
      elxp3 ) EHLEFGMZLZBHUGEINUHCOZDOZPZEQZUKUGLZRZDSCSBCDEFGUFUNBCDULUMBUMAUL
      BUMUIFLUJGLRAUIUJFGDUAUBKTJUCUDUETT $.
      $( [5-Mar-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y ps $.  $d z w ch $.  $d z w R $.
    2optocl.1 $e |- R = ( C X. D ) $.
    2optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    2optocl.3 $e |- ( <. z , w >. = B -> ( ps <-> ch ) ) $.
    2optocl.4 $e |- ( ( ( x e. C /\ y e. D ) /\ ( z e. C /\ w e. D ) ) ->
                   ph ) $.
    $( Implicit substitution of classes for ordered pairs. $)
    2optocl $p |- ( ( A e. R /\ B e. R ) -> ch ) $=
      ( wcel wi cv cop wceq imbi2d wa ex optocl com12 impcom ) ILQHLQZCUHBRUHCR
      FGIJKLMFSZGSZTIUABCUHOUBUHUIJQUJKQUCZBUKARUKBRDEHJKLMDSZESZTHUAABUKNUBULJ
      QUMKQUCUKAPUDUEUFUEUG $.
      $( [12-Mar-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d v u C $.  $d x y z w v u D $.
    $d x y z w v u F $.  $d z w v u R $.  $d x y ps $.  $d z w ch $.
    $d v u th $.
    3optocl.1 $e |- R = ( D X. F ) $.
    3optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    3optocl.3 $e |- ( <. z , w >. = B -> ( ps <-> ch ) ) $.
    3optocl.4 $e |- ( <. v , u >. = C -> ( ch <-> th ) ) $.
    3optocl.5 $e |- ( ( ( x e. D /\ y e. F ) /\ ( z e. D /\ w e. F )
                    /\ ( v e. D /\ u e. F ) ) -> ph ) $.
    $( Implicit substitution of classes for ordered pairs. $)
    3optocl $p |- ( ( A e. R /\ B e. R /\ C e. R ) -> th ) $=
      ( wcel wa wi cv cop wceq imbi2d 3expia 2optocl com12 optocl impcom 3impa
      ) KOUBZLOUBZMOUBZDUQUOUPUCZDURCUDURDUDIJMNPOQIUEZJUEZUFMUGCDURTUHURUSNUBU
      TPUBUCZCVAAUDVABUDVACUDEFGHKLNPOQEUEZFUEZUFKUGABVARUHGUEZHUEZUFLUGBCVASUH
      VBNUBVCPUBUCVDNUBVEPUBUCVAAUAUIUJUKULUMUN $.
      $( [12-Mar-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u S $.  $d x y ph $.  $d z w v u ps $.
    opbrop.1 $e |- ( ( ( z = A /\ w = B ) /\ ( v = C /\ u = D ) ) ->
                     ( ph <-> ps ) ) $.
    opbrop.2 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. z E. w E. v E. u ( ( x = <. z , w >. /\
                      y = <. v , u >. ) /\ ph ) ) } $.
    $( Ordered pair membership in a relation.  Special case. $)
    opbrop $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                   ( <. A , B >. R <. C , D >. <-> ps ) ) $=
      ( wcel wa cv wex cop wbr cxp wceq opex eleq1 anbi1d eqeq1 4exbidv anbi12d
      anbi2d brab copsex4g syl5bb opelxpi anim12i biantrurd bitr4d ) INQJNQRZKN
      QLNQRZRZIJUAZKLUAZMUBZVBNNUCZQZVCVEQZRZBRZBVDVHVBESFSUAZUDZVCGSHSUAZUDZRZ
      ARZHTGTFTETZRZVAVICSZVEQZDSZVEQZRZVRVJUDZVTVLUDZRZARZHTGTFTETZRVFWARZVKWD
      RZARZHTGTFTETZRVQCDVBVCMIJUEKLUEVRVBUDZWBWHWGWKWLVSVFWAVRVBVEUFUGWLWFWJEF
      GHWLWEWIAWLWCVKWDVRVBVJUHUGUGUIUJVTVCUDZWHVHWKVPWMWAVGVFVTVCVEUFUKWMWJVOE
      FGHWMWIVNAWMWDVMVKVTVCVLUHUKUGUIUJPULVAVPBVHABEFGHIJKLNNOUMUKUNVAVHBUSVFU
      TVGIJNNUOKLNNUOUPUQUR $.
      $( [5-Aug-1995] $)
  $}

  ${
    $d x y z A $.
    $( The cross product with the empty set is empty.  Part of Theorem 3.13(ii)
       of [Monk1] p. 37. $)
    xp0r $p |- ( (/) X. A ) = (/) $=
      ( vz vx vy c0 cxp cv wcel cop wceq wa wex elxp noel simprl mto nex 2false
      bitri eqriv ) BEAFZEBGZUAHUBCGZDGZIJZUCEHZUDAHZKKZDLZCLZUBEHZCDUBEAMUJUKU
      ICUHDUHUFUCNUEUFUGOPQQUBNRST $.
      $( [4-Jul-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( The empty set is not a member of a cross product. $)
    0nelxp $p |- -. (/) e. ( A X. B ) $=
      ( vx vy c0 cxp wcel cv cop wceq wa wex csn noel opi1 eleq2 mpbiri intnanr
      mto nex elxp mtbir ) EABFGECHZDHZIZJZUCAGUDBGKZKZDLZCLUICUHDUFUGUFUCMZEGZ
      UJNUFUKUJUEGUCUDOEUEUJPQSRTTCDEABUAUB $.
      $( [2-May-1996] $)

    $( A member of a cross product (ordered pair) doesn't contain the empty
       set. $)
    0nelelxp $p |- ( C e. ( A X. B ) -> -. (/) e. C ) $=
      ( vx vy cxp wcel cv cop wceq wa wex c0 elxp cvv vex notnoti opprc1b mtbi
      wn eleq2 mtbiri adantr exlimivv sylbi ) CABFGCDHZEHZIZJZUFAGUGBGKZKZELDLM
      CGZTZDECABNUKUMDEUIUMUJUIULMUHGZUFOGZTUNUODPQUFUGRSCUHMUAUBUCUDUE $.
      $( [15-Dec-2008] $)
  $}

  ${
    $d x y z $.
    $( Ordinal numbers and ordered pairs are disjoint collections.  This
       theorem can be used if we want to extend a set of ordinal numbers or
       ordered pairs with disjoint elements.  See also ~ snsn0non .  (The proof
       was shortened by Andrew Salmon, 27-Aug-2011.) $)
    onxpdisj $p |- ( On i^i ( _V X. _V ) ) = (/) $=
      ( vx con0 cvv cxp cin c0 wceq cv wcel wn disj on0eqel 0nelxp eleq1 mtbiri
      wo 0nelelxp con2i jaoi syl mprgbir ) BCCDZEFGAHZUBIZJZABABUBKUCBIUCFGZFUC
      IZPUEUCLUFUEUGUFUDFUBICCMUCFUBNOUDUGCCUCQRSTUA $.
      $( [27-Aug-2011] $) $( [1-Jun-2004] $)
  $}

  $( The class of ordinal numbers is not equal to the universe.  (The proof was
     shortened by Mario Carneiro, 10-Jan-2013.) $)
  onnev $p |- On =/= _V $=
    ( con0 cvv wne wceq wn csn wcel snsn0non snex eleq2 mpbiri mto df-ne mpbir
    c0 ) ABCABDZEPOFZFZAGZHPSRBGQIABRJKLABMN $.
    $( [10-Jan-2013] $) $( [16-Jun-2007] $)

  $( The class of ordinal numbers is not equal to the universe. $)
  onnevOLD $p |- On =/= _V $=
    ( cvv cxp c0 wne con0 cop wcel opelxpi mp2an ne0i ax-mp wceq ineq1 onxpdisj
    0ex cin incom inv1 eqtri 3eqtr3g eqcomd necon3i ) AABZCDZEADCCFZUCGZUDCAGZU
    GUFOOCCAAHIUCUEJKEAUCCEALZCUCUHEUCPAUCPZCUCEAUCMNUIUCAPUCAUCQUCRSTUAUBK $.
    $( [16-Jun-2007] $)

  $( Equality theorem for the relation predicate. $)
  releq $p |- ( A = B -> ( Rel A <-> Rel B ) ) $=
    ( wceq cvv cxp wss wrel sseq1 df-rel 3bitr4g ) ABCADDEZFBKFAGBGABKHAIBIJ $.
    $( [1-Aug-1994] $)

  ${
    releqi.1 $e |- A = B $.
    $( Equality inference for the relation predicate. $)
    releqi $p |- ( Rel A <-> Rel B ) $=
      ( wceq wrel wb releq ax-mp ) ABDAEBEFCABGH $.
      $( [8-Dec-2006] $)
  $}

  ${
    releqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for the relation predicate. $)
    releqd $p |- ( ph -> ( Rel A <-> Rel B ) ) $=
      ( wceq wrel wb releq syl ) ABCEBFCFGDBCHI $.
      $( [8-Mar-2014] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbrel.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for a relation. $)
    hbrel $p |- ( Rel A -> A. x Rel A ) $=
      ( wrel cvv cxp wss df-rel cv wcel ax-17 hbss hbxfrbi ) CECFFGZHACIABCODBJ
      OKALMN $.
      $( [31-Jan-2004] $)
  $}

  $( Subclass theorem for relation predicate.  Theorem 2 of [Suppes] p. 58. $)
  relss $p |- ( A C_ B -> ( Rel B -> Rel A ) ) $=
    ( wss cvv cxp wrel sstr2 df-rel 3imtr4g ) ABCBDDEZCAJCBFAFABJGBHAHI $.
    $( [15-Aug-1994] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( A subclass relationship depends only on a relation's ordered pairs.
       Theorem 3.2(i) of [Monk1] p. 33.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    ssrel $p |- ( Rel A -> ( A C_ B <->
                A. x A. y ( <. x , y >. e. A -> <. x , y >. e. B ) ) ) $=
      ( vz wrel wss cv cop wcel wi wal ssel alrimivv wceq wex eleq1 imbi12d cvv
      dfss2 biimprcd 2alimi 19.23vv sylib com23 alimdv df-rel elvv imbi2i albii
      a2d cxp 3bitri 3imtr4g com12 impbid2 ) CFZCDGZAHBHIZCJZUSDJZKZBLALZURVBAB
      CDUSMNVCUQURVCEHZCJZVDUSOZBPAPZKZELZVEVDDJZKZELUQURVCVHVKEVCVEVGVJVCVGVEV
      JVCVFVKKZBLALVGVKKVBVLABVFVKVBVFVEUTVJVAVDUSCQVDUSDQRUAUBVFVKABUCUDUEUKUF
      UQCSSULZGVEVDVMJZKZELVICUGECVMTVOVHEVNVGVEABVDUHUIUJUMECDTUNUOUP $.
      $( [27-Aug-2011] $) $( [2-Aug-1994] $)

    $( Extensionality principle for relations.  Theorem 3.2(ii) of [Monk1]
       p. 33. $)
    eqrel $p |- ( ( Rel A /\ Rel B ) -> ( A = B <->
                A. x A. y ( <. x , y >. e. A <-> <. x , y >. e. B ) ) ) $=
      ( wrel wa wss cv cop wcel wi wal wceq ssrel bi2anan9 eqss 2albiim 3bitr4g
      wb ) CEZDEZFCDGZDCGZFAHBHIZCJZUDDJZKBLALZUFUEKBLALZFCDMUEUFSBLALTUBUGUAUC
      UHABCDNABDCNOCDPUEUFABQR $.
      $( [2-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    relssi.1 $e |- Rel A $.
    relssi.2 $e |- ( <. x , y >. e. A -> <. x , y >. e. B ) $.
    $( Inference from subclass principle for relations. $)
    relssi $p |- A C_ B $=
      ( wss cv cop wcel wi wal wrel wb ssrel ax-mp ax-gen mpgbir ) CDGZAHBHIZCJ
      TDJKZBLZACMSUBALNEABCDOPUABFQR $.
      $( [31-Mar-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ph $.
    relssdv.1 $e |- ( ph -> Rel A ) $.
    relssdv.2 $e |- ( ph -> ( <. x , y >. e. A -> <. x , y >. e. B ) ) $.
    $( Deduction from subclass principle for relations. $)
    relssdv $p |- ( ph -> A C_ B ) $=
      ( wss cv cop wcel wi wal alrimivv wrel wb ssrel syl mpbird ) ADEHZBICIJZD
      KUAEKLZCMBMZAUBBCGNADOTUCPFBCDEQRS $.
      $( [11-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    eqrelriv.1 $e |- ( <. x , y >. e. A <-> <. x , y >. e. B ) $.
    $( Inference from extensionality principle for relations.  (Contributed by
       FL, 15-Oct-2012.) $)
    eqrelriv $p |- ( ( Rel A /\ Rel B ) -> A = B ) $=
      ( wrel wa wceq cv cop wcel wb wal gen2 eqrel mpbiri ) CFDFGCDHAIBIJZCKQDK
      LZBMAMRABENABCDOP $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    eqreliiv.1 $e |- Rel A $.
    eqreliiv.2 $e |- Rel B $.
    eqreliiv.3 $e |- ( <. x , y >. e. A <-> <. x , y >. e. B ) $.
    $( Inference from extensionality principle for relations. $)
    eqrelriiv $p |- A = B $=
      ( wrel wceq eqrelriv mp2an ) CHDHCDIEFABCDGJK $.
      $( [17-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    eqbrriv.1 $e |- Rel A $.
    eqbrriv.2 $e |- Rel B $.
    eqbrriv.3 $e |- ( x A y <-> x B y ) $.
    $( Inference from extensionality principle for relations. $)
    eqbrriv $p |- A = B $=
      ( cv wbr cop wcel df-br 3bitr3i eqrelriiv ) ABCDEFAHZBHZCIOPDIOPJZCKQDKGO
      PCLOPDLMN $.
      $( [12-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdv.1 $e |- Rel A $.
    eqrelrdv.2 $e |- Rel B $.
    eqrelrdv.3 $e |- ( ph -> ( <. x , y >. e. A <-> <. x , y >. e. B ) ) $.
    $( Deduce equality of relations from equivalence of membership.
       (Contributed by Rodolfo Medina, 10-Oct-2010.) $)
    eqrelrdv $p |- ( ph -> A = B ) $=
      ( cv cop wcel wb wal wceq alrimivv wrel eqrel mp2an sylibr ) ABICIJZDKTEK
      LZCMBMZDENZAUABCHODPEPUCUBLFGBCDEQRS $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdv2.1 $e |- ( ph
       -> ( <. x , y >. e. A <-> <. x , y >. e. B ) ) $.
    $( A version of ~ eqrelrdv .  (Contributed by Rodolfo Medina,
       10-Oct-2010.) $)
    eqrelrdv2 $p |- ( ( ( Rel A /\ Rel B ) /\ ph ) -> A = B ) $=
      ( wrel wa wceq cv cop wcel wb wal alrimivv adantl eqrel adantr mpbird ) D
      GEGHZAHDEIZBJCJKZDLUBELMZCNBNZAUDTAUCBCFOPTUAUDMABCDEQRS $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.
    $( A subclass relationship determined by ordered triples.  Use ~ relrelss
       to express the antecedent in terms of the relation predicate.  (The
       proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    ssrelrel $p |- ( A C_ ( ( _V X. _V ) X. _V ) -> ( A C_ B <->
                   A. x A. y A. z ( <. <. x , y >. , z >. e. A
                       -> <. <. x , y >. , z >. e. B ) ) ) $=
      ( vw cvv cxp wss cv cop wcel wi wal ssel alrimiv wex eleq1 sylib dfss2
      alrimivv wceq elvvv imbi12d biimprcd alimi 19.23v 2alimi syl5bi com23 a2d
      19.23vv alimdv 3imtr4g com12 impbid2 ) DGGHGHZIZDEIZAJBJKCJKZDLZUTELZMZCN
      ZBNANZUSVDABUSVCCDEUTOPUAVEURUSVEFJZDLZVFUQLZMZFNVGVFELZMZFNURUSVEVIVKFVE
      VGVHVJVEVHVGVJVHVFUTUBZCQZBQAQZVEVKABCVFUCVEVMVKMZBNANVNVKMVDVOABVDVLVKMZ
      CNVOVCVPCVLVKVCVLVGVAVJVBVFUTDRVFUTERUDUEUFVLVKCUGSUHVMVKABULSUIUJUKUMFDU
      QTFDETUNUOUP $.
      $( [27-Aug-2011] $) $( [17-Dec-2008] $)

    $( Extensionality principle for ordered triples (used by 2-place operations
       ~ df-oprab ), analogous to ~ eqrel .  Use ~ relrelss to express the
       antecedent in terms of the relation predicate. $)
    eqrelrel $p |- ( ( A u. B ) C_ ( ( _V X. _V ) X. _V ) -> ( A = B <->
                   A. x A. y A. z ( <. <. x , y >. , z >. e. A
                       <-> <. <. x , y >. , z >. e. B ) ) ) $=
      ( cun cvv cxp wss wa wceq cv cop wcel wb wal unss wi ssrelrel bi2anan9
      eqss 2albiim albii 19.26 bitri 3bitr4g sylbir ) DEFGGHGHZIDUHIZEUHIZJZDEK
      ZALBLMCLMZDNZUMENZOCPBPZAPZODEUHQUKDEIZEDIZJUNUORCPBPZAPZUOUNRCPBPZAPZJZU
      LUQUIURVAUJUSVCABCDESABCEDSTDEUAUQUTVBJZAPVDUPVEAUNUOBCUBUCUTVBAUDUEUFUG
      $.
      $( [17-Dec-2008] $)
  $}

  ${
    $d x y A $.
    $( A member of a relation is an ordered pair. $)
    elrel $p |- ( ( Rel R /\ A e. R ) -> E. x E. y A = <. x , y >. ) $=
      ( wrel wcel wa cvv cxp cv cop wceq wex wss df-rel biimpi sseld elvv sylib
      imp ) DEZCDFZGCHHIZFZCAJBJKLBMAMUAUBUDUADUCCUADUCNDOPQTABCRS $.
      $( [17-Sep-2006] $)
  $}

  ${
    relsn.1 $e |- A e. _V $.
    $( A singleton is a relation iff it is an ordered pair. $)
    relsn $p |- ( Rel { A } <-> A e. ( _V X. _V ) ) $=
      ( csn wrel cvv cxp wss wcel df-rel snss bitr4i ) ACZDLEEFZGAMHLIAMBJK $.
      $( [24-Sep-2013] $)

    $( A singleton of an ordered pair is a relation. $)
    relsnop $p |- Rel { <. A , B >. } $=
      ( cop csn wrel cvv cxp wcel opelvv opex relsn mpbir ) ABDZEFNGGHIABCJNABK
      LM $.
      $( [17-May-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
    $( Subset theorem for cross product.  Generalization of Theorem 101 of
       [Suppes] p. 52.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    xpss12 $p |- ( ( A C_ B /\ C C_ D ) -> ( A X. C ) C_ ( B X. D ) ) $=
      ( vx vy wss wa cv wcel copab cxp ssel im2anan9 ssopab2dv df-xp 3sstr4g )
      ABGZCDGZHZEIZAJZFIZCJZHZEFKUABJZUCDJZHZEFKACLBDLTUEUHEFRUBUFSUDUGABUAMCDU
      CMNOEFACPEFBDPQ $.
      $( [27-Aug-2011] $) $( [26-Aug-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( A cross product is included in the ordered pair universe.  Exercise 3 of
       [TakeutiZaring] p. 25. $)
    xpss $p |- ( A X. B ) C_ ( _V X. _V ) $=
      ( cvv wss cxp ssv xpss12 mp2an ) ACDBCDABECCEDAFBFACBCGH $.
      $( [2-Aug-1994] $)
  $}

  $( A cross product is a relation.  Theorem 3.13(i) of [Monk1] p. 37. $)
  relxp $p |- Rel ( A X. B ) $=
    ( cxp wrel cvv wss xpss df-rel mpbir ) ABCZDJEECFABGJHI $.
    $( [2-Aug-1994] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
    $( Subset theorem for cross product.  Generalization of Theorem 101 of
       [Suppes] p. 52.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    xpss12OLD $p |- ( ( A C_ B /\ C C_ D ) -> ( A X. C ) C_ ( B X. D ) ) $=
      ( vx vy wss wa cxp wrel relxp a1i cv wcel cop im2anan9 vex opelxp 3imtr4g
      ssel relssdv ) ABGZCDGZHZEFACIZBDIZUEJUDACKLUDEMZANZFMZCNZHUGBNZUIDNZHUGU
      IOZUENUMUFNUBUHUKUCUJULABUGTCDUITPUGUIACFQZRUGUIBDUNRSUA $.
      $( [27-Aug-2011] $) $( [26-Aug-1995] $)
  $}

  $( Subset relation for cross product.  (Contributed by Jeff Hankins,
     30-Aug-2009.) $)
  xpss1 $p |- ( A C_ B -> ( A X. C ) C_ ( B X. C ) ) $=
    ( wss cxp ssid xpss12 mpan2 ) ABDCCDACEBCEDCFABCCGH $.
    $( [8-Mar-2011] $) $( [14-Aug-2009] $)

  $( Subset relation for cross product.  (Contributed by Jeff Hankins,
     30-Aug-2009.) $)
  xpss2 $p |- ( A C_ B -> ( C X. A ) C_ ( C X. B ) ) $=
    ( wss cxp ssid xpss12 mpan ) CCDABDCAECBEDCFCCABGH $.
    $( [8-Mar-2011] $) $( [14-Aug-2009] $)

  ${
    $d A x y z $.  $d B x y z $.
    $( A cross product is included in the power of the power of the union of
       its arguments. $)
    xpsspw $p |- ( A X. B ) C_ ~P ~P ( A u. B ) $=
      ( vz vx vy cun cpw cv wcel wss wceq wa wex csn cpr snssi sseq1 syl5ibrcom
      syl syl5eqss cxp cop elxpi df-op wo elpr ssun3 adantr df-pr ssun4 anim12i
      vex unss sylib jaod elpw syl6ibr syl5bi ssrdv biimpar sylan2 sylibr ssriv
      exlimivv ) CABUAZABFZGZGZCHZVEIZVIVGJZVIVHIVJVIDHZEHZUBZKZVLAIZVMBIZLZLZE
      MDMVKDEVIABUCVSVKDEVRVOVNVGJZVKVRVNVLNZVLVMOZOZVGVLVMUDVRCWCVGVIWCIVIWAKZ
      VIWBKZUEZVRVIVGIZVIWAWBCULZUFVRWFVIVFJZWGVRWDWIWEVRWIWDWAVFJZVPWJVQVPWAAJ
      WJVLAPWAABUGSZUHVIWAVFQRVRWIWEWBVFJVRWBWAVMNZFZVFVLVMUIVRWJWLVFJZLWMVFJVP
      WJVQWNWKVQWLBJWNVMBPWLBAUJSUKWAWLVFUMUNTVIWBVFQRUOVIVFWHUPUQURUSTVOVKVTVI
      VNVGQUTVAVDSVIVGWHUPVBVC $.
      $( [13-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A cross product is included in the power of the power of the union of
       its arguments. $)
    xpsspwOLD $p |- ( A X. B ) C_ ~P ~P ( A u. B ) $=
      ( vx vy cxp cun cpw relxp cv cop wcel wa csn cpr wss snssi syl elpw sylib
      sylibr opelxp ssun3 snex adantr df-pr ssun4 anim12i unss syl5eqss zfpair2
      vex jca prex df-op eleq1i prss 3bitr4ri sylbi relssi ) CDABEZABFZGZGZABHC
      IZDIZJZUTKVDAKZVEBKZLZVFVCKZVDVEABDUKUAVIVDMZVBKZVDVENZVBKZLZVJVIVLVNVGVL
      VHVGVKVAOZVLVGVKAOVPVDAPVKABUBQZVKVAVDUCZRTUDVIVMVAOVNVIVMVKVEMZFZVAVDVEU
      EVIVPVSVAOZLVTVAOVGVPVHWAVQVHVSBOWAVEBPVSBAUFQUGVKVSVAUHSUIVMVACDUJZRTULV
      KVMNZVCKWCVBOVJVOWCVBVKVMUMRVFWCVCVDVEUNUOVKVMVBVRWBUPUQSURUS $.
      $( [13-Sep-2006] $)
  $}

  $( The double class union of a cross product is included in the union of its
     arguments. $)
  unixpss $p |- U. U. ( A X. B ) C_ ( A u. B ) $=
    ( cxp cuni cun cpw wss xpsspw uniss ax-mp unipw sseqtri ) ABCZDZDZABEZFZDZP
    NQGORGNQFZDZQMSGNTGABHMSIJQKLNQIJPKL $.
    $( [16-Sep-2006] $)

  $( The cross product of two sets is a set.  Proposition 6.2 of
     [TakeutiZaring] p. 23. $)
  xpexg $p |- ( ( A e. V /\ B e. W ) -> ( A X. B ) e. _V ) $=
    ( wcel wa cun cpw cvv cxp unexg pwexg syl wss xpsspw ssexg mpan 3syl ) ACEB
    DEFZABGZHZIEZUAHZIEZABJZIEZSTIEUBABCDKTILMUAILUEUCNUDUFABOUEUCIPQR $.
    $( [14-Aug-1994] $)

  ${
    xpex.1 $e |- A e. _V $.
    xpex.2 $e |- B e. _V $.
    $( The cross product of two sets is a set.  Proposition 6.2 of
       [TakeutiZaring] p. 23. $)
    xpex $p |- ( A X. B ) e. _V $=
      ( cvv wcel cxp xpexg mp2an ) AEFBEFABGEFCDABEEHI $.
      $( [14-Aug-1994] $)
  $}

  $( The union of two relations is a relation.  Compare Exercise 5 of
     [TakeutiZaring] p. 25. $)
  relun $p |- ( Rel ( A u. B ) <-> ( Rel A /\ Rel B ) ) $=
    ( cvv cxp wss wa cun wrel unss df-rel anbi12i 3bitr4ri ) ACCDZEZBMEZFABGZME
    AHZBHZFPHABMIQNROAJBJKPJL $.
    $( [12-Aug-1994] $)

  $( The intersection with a relation is a relation. $)
  relin1 $p |- ( Rel A -> Rel ( A i^i B ) ) $=
    ( cin wss wrel wi inss1 relss ax-mp ) ABCZADAEJEFABGJAHI $.
    $( [16-Aug-1994] $)

  $( The intersection with a relation is a relation. $)
  relin2 $p |- ( Rel B -> Rel ( A i^i B ) ) $=
    ( cin wss wrel wi inss2 relss ax-mp ) ABCZBDBEJEFABGJBHI $.
    $( [17-Jan-2006] $)

  $( A difference cutting down a relation is a relation. $)
  reldif $p |- ( Rel A -> Rel ( A \ B ) ) $=
    ( cdif wss wrel wi difss relss ax-mp ) ABCZADAEJEFABGJAHI $.
    $( [31-Mar-1998] $)

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( An indexed union is a relation iff each member of its indexed family is
       a relation. $)
    reliun $p |- ( Rel U_ x e. A B <-> A. x e. A Rel B ) $=
      ( vy ciun wrel cv wcel wrex cab cvv cxp wss wral df-iun releqi df-rel wal
      wi 3bitri abss dfss2 bitri ralbii ralcom4 r19.23v albii bitr4i ) ABCEZFDG
      ZCHZABIZDJZFUMKKLZMZCFZABNZUIUMADBCOPUMQUOULUJUNHZSZDRZUQULDUNUAUQUKURSZD
      RZABNVAABNZDRUTUPVBABUPCUNMVBCQDCUNUBUCUDVAADBUEVCUSDUKURABUFUGTUHT $.
      $( [19-Dec-2008] $)
  $}

  $( An indexed intersection is a relation if if at least one of the member of
     the indexed family is a relation. $)
  reliin $p |- ( E. x e. A Rel B -> Rel |^|_ x e. A B ) $=
    ( cvv cxp wss wrex ciin wrel iinss df-rel rexbii 3imtr4i ) CDDEZFZABGABCHZN
    FCIZABGPIABCNJQOABCKLPKM $.
    $( [8-Mar-2014] $)

  ${
    $d x A $.
    $( The union of a class is a relation iff any member is a relation.
       Exercise 6 of [TakeutiZaring] p. 25 and its converse. $)
    reluni $p |- ( Rel U. A <-> A. x e. A Rel x ) $=
      ( cuni wrel cv ciun wral uniiun releqi reliun bitri ) BCZDABAEZFZDMDABGLN
      ABHIABMJK $.
      $( [21-Dec-2008] $) $( [13-Aug-2004] $)

    $( The intersection of a class is a relation if at least one member is a
       relation. $)
    relint $p |- ( E. x e. A Rel x -> Rel |^| A ) $=
      ( cv wrel wrex ciin cint reliin intiin releqi sylibr ) ACZDABEABLFZDBGZDA
      BLHNMABIJK $.
      $( [8-Mar-2014] $)
  $}

  $( The empty set is a relation. $)
  rel0 $p |- Rel (/) $=
    ( c0 wrel cvv cxp wss 0ss df-rel mpbir ) ABACCDZEIFAGH $.
    $( [26-Apr-1998] $)

  ${
    $d ph z $.  $d u v x z $.  $d u v y z $.
    relopabi.1 $e |- A = { <. x , y >. | ph } $.
    $( A class of ordered pairs is a relation.  (The proof was shortened by
       Mario Carneiro, 21-Dec-2013.) $)
    relopabi $p |- Rel A $=
      ( vz wrel cvv cxp wss cv cop wceq wa wex cab copab df-opab wcel vex eqtri
      opelxp mpbir2an eleq1 mpbiri adantr exlimivv abssi eqsstri df-rel mpbir )
      DGDHHIZJDFKZBKZCKZLZMZANZCOBOZFPZULDABCQUTEABCFRUAUSFULURUMULSZBCUQVAAUQV
      AUPULSZVBUNHSUOHSUNUOHHCTZUBBTVCUCUMUPULUDUEUFUGUHUIDUJUK $.
      $( [21-Dec-2013] $)
  $}

  $( A class of ordered pairs is a relation.  (Unnecessary distinct variable
     restrictions were removed by Alan Sare, 9-Jul-2013.)  (The proof was
     shortened by Mario Carneiro, 21-Dec-2013.) $)
  relopab $p |- Rel { <. x , y >. | ph } $=
    ( copab eqid relopabi ) ABCABCDZGEF $.
    $( [9-Jul-2013] $) $( [8-Mar-1995] $)

  ${
    $d w x y z A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d ph z w $.
    $d ps z w $.
    $( Obsolete proof of ~ relopab as of 21-Dec-2013. $)
    relopabOLD $p |- Rel { <. x , y >. | ph } $=
      ( copab wrel cvv cxp wss cv wcel wa pm3.2i ssopab2i df-xp sseqtr4i df-rel
      vex a1i mpbir ) ABCDZETFFGZHTBIFJZCIFJZKZBCDUAAUDBCUDAUBUCBQCQLRMBCFFNOTP
      S $.
      $( [8-Mar-1995] $)

    $( The identity relation is a relation.  Part of Exercise 4.12(p) of
       [Mendelson] p. 235. $)
    reli $p |- Rel _I $=
      ( vx vy cv wceq cid dfid3 relopabi ) ACBCDABEABFG $.
      $( [21-Dec-2013] $) $( [26-Apr-1998] $)

    $( The membership relation is a relation. $)
    rele $p |- Rel _E $=
      ( vx vy cv wcel cep df-eprel relopabi ) ACBCDABEABFG $.
      $( [21-Dec-2013] $) $( [26-Apr-1998] $)

    $( A relation expressed as an ordered pair abstraction. $)
    opabid2 $p |- ( Rel A -> { <. x , y >. | <. x , y >. e. A } = A ) $=
      ( vz vw wrel cv cop wcel copab wceq wb wal vex opeq1 eleq1d opelopab gen2
      opeq2 relopab eqrel mpan mpbiri ) CFZAGZBGZHZCIZABJZCKZDGZEGZHZUIIUMCIZLZ
      EMDMZUODEUHUKUFHZCIUNABUKULDNENUEUKKUGUQCUEUKUFOPUFULKUQUMCUFULUKSPQRUIFU
      DUJUPLUHABTDEUICUAUBUC $.
      $( [11-Dec-2006] $)

    $( Intersection of two ordered pair class abstractions. $)
    inopab $p |- ( { <. x , y >. | ph } i^i { <. x , y >. | ps } ) =
               { <. x , y >. | ( ph /\ ps ) } $=
      ( vz vw copab cin wrel relopab relin1 ax-mp cop wcel wsbc sban opelopabsb
      wa cv sbbii anbi12i 3bitr4ri elin 3bitr4i eqrelriiv ) EFACDGZBCDGZHZABRZC
      DGZUFIUHIACDJUFUGKLUICDJESZFSZMZUFNZUMUGNZRZUICUKOZDULOZUMUHNUMUJNACUKOZB
      CUKOZRZDULOUSDULOZUTDULOZRURUPUSUTDFPUQVADFABCEPTUNVBUOVCACDEFQBCDEFQUAUB
      UMUFUGUCUICDEFQUDUE $.
      $( [30-Sep-2002] $)

    $( The intersection of two cross products.  Exercise 9 of [TakeutiZaring]
       p. 25.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    inxp $p |- ( ( A X. B ) i^i ( C X. D ) ) =
                   ( ( A i^i C ) X. ( B i^i D ) ) $=
      ( vx vy cv wcel wa copab cin cxp inopab elin anbi12i bitr4i opabbii eqtri
      an4 df-xp ineq12i 3eqtr4i ) EGZAHZFGZBHZIZEFJZUCCHZUEDHZIZEFJZKZUCACKZHZU
      EBDKZHZIZEFJZABLZCDLZKUNUPLUMUGUKIZEFJUSUGUKEFMVBUREFVBUDUIIZUFUJIZIURUDU
      FUIUJSUOVCUQVDUCACNUEBDNOPQRUTUHVAULEFABTEFCDTUAEFUNUPTUB $.
      $( [27-Aug-2011] $) $( [3-Aug-1994] $)

    $( Distributive law for cross product over intersection.  Theorem 102 of
       [Suppes] p. 52. $)
    xpindi $p |- ( A X. ( B i^i C ) ) = ( ( A X. B ) i^i ( A X. C ) ) $=
      ( cxp cin inxp inidm xpeq1i eqtr2i ) ABDACDEAAEZBCEZDAKDABACFJAKAGHI $.
      $( [26-Sep-2004] $)

    $( Distributive law for cross product over intersection.  Similar to
       Theorem 102 of [Suppes] p. 52. $)
    xpindir $p |- ( ( A i^i B ) X. C ) = ( ( A X. C ) i^i ( B X. C ) ) $=
      ( cxp cin inxp inidm xpeq2i eqtr2i ) ACDBCDEABEZCCEZDJCDACBCFKCJCGHI $.
      $( [26-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d x y ph $.
    opabbi2dv.1 $e |- Rel A $.
    opabbi2dv.3 $e |- ( ph -> ( <. x , y >. e. A <-> ps ) ) $.
    $( Deduce equality of a relation and an ordered-pair class builder.
       Compare ~ abbi2dv . $)
    opabbi2dv $p |- ( ph -> A = { <. x , y >. | ps } ) $=
      ( cv cop wcel copab wrel wceq opabid2 ax-mp opabbidv syl5eqr ) AECHDHIEJZ
      CDKZBCDKELSEMFCDENOARBCDGPQ $.
      $( [24-Feb-2014] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.
    relop.1 $e |- A e. _V $.
    relop.2 $e |- B e. _V $.
    $( A necessary and sufficient condition for a Kuratowski ordered pair to be
       a relation. $)
    relop $p |- ( Rel <. A , B >.
             <-> E. x E. y ( A = { x } /\ B = { x , y } ) ) $=
      ( vz vw vv cop cv wceq cpr wa wex wi wal vex bitri eqeq2d cvv cxp wss csn
      wrel df-rel wcel dfss2 wo elop elvv imbi12i jaob albii 19.26 3bitri eqeq1
      snex eqcom opeqsn syl6bb 2exbidv imbi12d cla4v cbvexv 19.41v equcom exbii
      sneq a9e mpbi mpbiran eqid a1bi 3bitr2ri sylib prex mpi opeqpr idd preqsn
      eqtr2 simplbi syl dfsn2 preq2 syl5req syl5eq anbi12d biimpd adantr expcom
      exp3a com12 mpd imp3a syl5bi 2eximdv exlimiv imp syl2an sylbi simpr equid
      jaod jctl sylibr eqtr4d opeq12 cla42ev adantlr preq12 biimpa df-op jaodan
      syl6eqr ex 3imtr4g ssrdv exlimivv impbii ) CDJZUEYBUAUAUBZUCZCAKZUDZLZDYE
      BKZMZLZNZBOAOZYBUFYDYLYDGKZCUDZLZYMYEYHJZLZBOAOZPZGQZYMCDMZLZYRPZGQZNZYLY
      DYMYBUGZYMYCUGZPZGQYSUUCNZGQUUEGYBYCUHUUHUUIGUUHYOUUBUIZYRPUUIUUFUUJUUGYR
      YMCDGRUJZABYMUKULYOYRUUBUMSUNYSUUCGUOUPYTCHKZUDZLZHOZUUAYPLZBOAOZYLUUDYTY
      NYNLZYEYHLZYGNZBOZAOZPZUUOYSUVCGYNCURYOYOUURYRUVBYMYNYNUQYOYQUUTABYOYQYNY
      PLZUUTYMYNYPUQUVDYPYNLUUTYNYPUSYEYHCARZBRZEUTSVAVBVCVDUUOYGAOUVBUVCUUNYGH
      AUULYELZUUMYFCUULYEVITVEUVAYGAUVAUUSBOZYGUUSYGBVFYHYELZBOUVHBAVJUVIUUSBBA
      VGVHVKVLVHUURUVBYNVMVNVOVPUUDUUAUUALZUUQUUAVMUUCUVJUUQPGUUACDVQUUBUUBUVJY
      RUUQYMUUAUUAUQUUBYQUUPABYMUUAYPUQVBVCVDVRUUOUUQYLUUNUUQYLPHUUNUUPYKABUUPY
      KCYILZDYFLZNZUIZUUNYKUUPYPUUALUVNUUAYPUSYEYHCDEFVSSUUNYKYKUVMUUNYKVTUUNUV
      KUVLYKUVKUUNUVLYKPZUVKUUNNZUUSUVOUVPYIUUMLZUUSCYIUUMWBUVQUUSYHUULLYEYHUUL
      UVEUVFHRWAWCWDUVKUUSUVOPUUNUUSUVKUVOUUSUVKUVLYKUUSUVMYKUUSUVKYGUVLYJUUSYI
      YFCUUSYFYEYEMZYIYEWEZYEYHYEWFZWGTUUSYFYIDUUSYFUVRYIUVSUVTWHTWIWJWMWNWKWOW
      LWPXEWQWRWSWTXAXBYKYDABYKGYBYCYKUUJYMUULIKZJZLZIOHOZUUFUUGYKUUJUWDYKYOUWD
      UUBYGYOUWDYJYGYONZYMYEYEJZLZUWDUWEYMYNUWFYGYOXCYGUWFYNLZYOYGYEYELZYGNUWHY
      GUWIAXDXFYEYECUVEUVEEUTXGWKXHUWCUWGHIYEYEUVEUVEUVGUWAYELNUWBUWFYMUULUWAYE
      YEXITXJWDXKYKUUBNZYQUWDUWJYMYFYIMZYPYKUUBYMUWKLYKUUAUWKYMCDYFYIXLTXMYEYHX
      NXPUWCYQHIYEYHUVEUVFUVGUWAYHLNUWBYPYMUULUWAYEYHXITXJWDXOXQUUKHIYMUKXRXSXT
      YAS $.
      $( [3-Jun-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( For sets, the identity relation is the same as equality.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    ideqg $p |- ( B e. V -> ( A _I B <-> A = B ) ) $=
      ( vx vy wcel cid wbr wceq cvv wa brrelexi adantl simpl jca eleq1 biimparc
      reli elex cv syl eqeq1 eqeq2 df-id brabg pm5.21nd ) BCFZABGHZABIZAJFZUGKU
      GUHKUJUGUHUJUGABGRLMUGUHNOUGUIKZUJUGUKACFZUJUIULUGABCPQACSUAUGUINODTZETZI
      AUNIUIDEABJCGUMAUNUBUNBAUCDEUDUEUF $.
      $( [27-Aug-2011] $) $( [30-Apr-2004] $)
  $}

  ${
    ideq.1 $e |- B e. _V $.
    $( For sets, the identity relation is the same as equality. $)
    ideq $p |- ( A _I B <-> A = B ) $=
      ( cvv wcel cid wbr wceq wb ideqg ax-mp ) BDEABFGABHICABDJK $.
      $( [1-Jun-2008] $) $( [13-Aug-1995] $)
  $}

  ${
    $d x A $.
    $( A set is identical to itself.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    ididg $p |- ( A e. V -> A _I A ) $=
      ( wcel cid wbr wceq eqid ideqg mpbiri ) ABCAADEAAFAGAABHI $.
      $( [27-Aug-2011] $) $( [28-May-2008] $)
  $}

  $( The second member of an ordered pair of classes in a cross product exists
     when the order pair doesn't belong to ` _I ` .  (The proof was shortened
     by Andrew Salmon, 27-Aug-2011.) $)
  opelxpex2 $p |- ( <. A , B >. e. ( ( C X. D ) \ _I ) -> B e. _V ) $=
    ( cop cxp cid cdif wn wa cvv eldif wi opelxp1 wbr ididg df-br brprc syl5bbr
    wcel syl5ibrcom con1d syl imp sylbi ) ABEZCDFZGHTUFUGTZUFGTZIZJBKTZUFUGGLUH
    UJUKUHACTZUJUKMABCDNULUKUIULUIUKIZAAGOZACPUIABGOUMUNABGQABGRSUAUBUCUDUE $.
    $( [27-Aug-2011] $) $( [28-May-2008] $)

  $( Two ways of expressing set existence.  (The proof was shortened by Andrew
     Salmon, 27-Aug-2011.) $)
  issetid $p |- ( A e. _V <-> A _I _V ) $=
    ( cvv wcel cid wbr ididg wn wb vprc brprc ax-mp sylibr reli brrelexi impbii
    ) ABCZABDEZPAADEZQABFBBCGQRHIABDJKLABDMNO $.
    $( [27-Aug-2011] $) $( [16-Feb-2008] $)

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    $( Subclass theorem for composition.  (Contributed by FL, 30-Dec-2010.) $)
    coss1 $p |- ( A C_ B -> ( A o. C ) C_ ( B o. C ) ) $=
      ( vx vy vz wss cv wbr wa wex copab id ssbrd anim2d eximdv ssopab2dv df-co
      ccom 3sstr4g ) ABGZDHEHZCIZUBFHZAIZJZEKZDFLUCUBUDBIZJZEKZDFLACSBCSUAUGUJD
      FUAUFUIEUAUEUHUCUAABUBUDUAMNOPQDFEACRDFEBCRT $.
      $( [30-Dec-2010] $)

    $( Subclass theorem for composition. $)
    coss2 $p |- ( A C_ B -> ( C o. A ) C_ ( C o. B ) ) $=
      ( vx vy vz wss cv wbr wa wex copab id ssbrd anim1d eximdv ssopab2dv df-co
      ccom 3sstr4g ) ABGZDHZEHZAIZUCFHCIZJZEKZDFLUBUCBIZUEJZEKZDFLCASCBSUAUGUJD
      FUAUFUIEUAUDUHUEUAABUBUCUAMNOPQDFECARDFECBRT $.
      $( [5-Apr-2013] $)
  $}

  $( Equality theorem for composition of two classes. $)
  coeq1 $p |- ( A = B -> ( A o. C ) = ( B o. C ) ) $=
    ( wss wa ccom wceq coss1 anim12i eqss 3imtr4i ) ABDZBADZEACFZBCFZDZONDZEABG
    NOGLPMQABCHBACHIABJNOJK $.
    $( [3-Jan-1997] $)

  $( Equality theorem for composition of two classes. $)
  coeq2 $p |- ( A = B -> ( C o. A ) = ( C o. B ) ) $=
    ( wss wa ccom wceq coss2 anim12i eqss 3imtr4i ) ABDZBADZECAFZCBFZDZONDZEABG
    NOGLPMQABCHBACHIABJNOJK $.
    $( [3-Jan-1997] $)

  ${
    coeq1i.1 $e |- A = B $.
    $( Equality inference for composition of two classes. $)
    coeq1i $p |- ( A o. C ) = ( B o. C ) $=
      ( wceq ccom coeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [16-Nov-2000] $)

    $( Equality inference for composition of two classes. $)
    coeq2i $p |- ( C o. A ) = ( C o. B ) $=
      ( wceq ccom coeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [16-Nov-2000] $)
  $}

  ${
    coeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for composition of two classes. $)
    coeq1d $p |- ( ph -> ( A o. C ) = ( B o. C ) ) $=
      ( wceq ccom coeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [16-Nov-2000] $)

    $( Equality deduction for composition of two classes. $)
    coeq2d $p |- ( ph -> ( C o. A ) = ( C o. B ) ) $=
      ( wceq ccom coeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [16-Nov-2000] $)
  $}

  ${
    coeq12i.1 $e |- A = B $.
    coeq12i.2 $e |- C = D $.
    $( Equality inference for composition of two classes.  (Contributed by FL,
       7-Jun-2012.) $)
    coeq12i $p |- ( A o. C ) = ( B o. D ) $=
      ( ccom coeq1i coeq2i eqtri ) ACGBCGBDGABCEHCDBFIJ $.
      $( [7-Jun-2012] $)
  $}

  ${
    coeq12d.1 $e |- ( ph -> A = B ) $.
    coeq12d.2 $e |- ( ph -> C = D ) $.
    $( Equality deduction for composition of two classes.  (Contributed by FL,
       7-Jun-2012.) $)
    coeq12d $p |- ( ph -> ( A o. C ) = ( B o. D ) ) $=
      ( ccom coeq1d coeq2d eqtrd ) ABDHCDHCEHABCDFIADECGJK $.
      $( [7-Jun-2012] $)
  $}

  ${
    $d y z w v A $.  $d y z w v B $.  $d x y z w v $.
    hbco.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbco.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for function value. $)
    hbco $p |- ( y e. ( A o. B ) -> A. x y e. ( A o. B ) ) $=
      ( vz vv vw ccom cv wbr wa wex copab df-co wcel ax-17 hbbr hban hbxfreq
      hbex hbopab ) ABCDJGKZHKZDLZUEIKZCLZMZHNZGIOGIHCDPUJGIABUIAHUFUHAABUDUEDB
      KZUDQARFUKUEQARZSABUEUGCULEUKUGQARSTUBUCUA $.
      $( [1-Sep-1999] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    opelco.1 $e |- A e. _V $.
    opelco.2 $e |- B e. _V $.
    $( Ordered pair membership in a composition.  (The proof was shortened by
       Andrew Salmon, 27-Aug-2011.) $)
    opelco $p |- ( <. A , B >. e. ( C o. D ) <-> E. x ( A D x /\ x C B ) ) $=
      ( vy vz cop ccom wcel wbr cv wa wex df-br wceq breq1 exbidv anbi1d anbi2d
      breq2 df-co brab bitr3i ) BCJDEKZLBCUGMBANZEMZUHCDMZOZAPZBCUGQHNZUHEMZUHI
      NZDMZOZAPUIUPOZAPULHIBCUGFGUMBRZUQURAUSUNUIUPUMBUHESUATUOCRZURUKAUTUPUJUI
      UOCUHDUCUBTHIADEUDUEUF $.
      $( [27-Aug-2011] $) $( [27-Dec-1996] $)

    $( Binary relation on a composition. $)
    brco $p |- ( A ( C o. D ) B <-> E. x ( A D x /\ x C B ) ) $=
      ( ccom wbr cop wcel cv wa wex df-br opelco bitri ) BCDEHZIBCJRKBALZEISCDI
      MANBCROABCDEFGPQ $.
      $( [21-Sep-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    $( Ordered pair membership in a composition. $)
    opelco2g $p |- ( ( A e. V /\ B e. W ) -> ( <. A , B >. e. ( C o. D ) <->
                   E. x ( <. A , x >. e. D /\ <. x , B >. e. C ) ) ) $=
      ( vy vz wcel wa cop cv wbr wex wb wceq eleq1d exbidv bibi12d opeq1 anbi1d
      ccom breq1 opeq2 breq2 anbi2d opelco vtocl2g df-br anbi12i exbii syl6bb
      vex ) BFJCGJKBCLZDEUCZJZBAMZENZURCDNZKZAOZBURLEJZURCLDJZKZAOHMZIMZLZUPJZV
      FURENZURVGDNZKZAOZPBVGLZUPJZUSVKKZAOZPUQVBPHIBCFGVFBQZVIVOVMVQVRVHVNUPVFB
      VGUARVRVLVPAVRVJUSVKVFBUREUDUBSTVGCQZVOUQVQVBVSVNUOUPVGCBUERVSVPVAAVSVKUT
      USVGCURDUFUGSTAVFVGDEHUNIUNUHUIVAVEAUSVCUTVDBUREUJURCDUJUKULUM $.
      $( [27-Jan-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Subset theorem for converse. $)
    cnvss $p |- ( A C_ B -> `' A C_ `' B ) $=
      ( vy vx wss wbr copab ccnv cop wcel ssel 3imtr4g ssopab2dv df-cnv 3sstr4g
      cv df-br ) ABEZCPZDPZAFZDCGSTBFZDCGAHBHRUAUBDCRSTIZAJUCBJUAUBABUCKSTAQSTB
      QLMDCANDCBNO $.
      $( [22-Mar-1998] $)
  $}

  $( Equality theorem for converse. $)
  cnveq $p |- ( A = B -> `' A = `' B ) $=
    ( wss wa ccnv wceq cnvss anim12i eqss 3imtr4i ) ABCZBACZDAEZBEZCZNMCZDABFMN
    FKOLPABGBAGHABIMNIJ $.
    $( [13-Aug-1995] $)

  ${
    cnveqi.1 $e |- A = B $.
    $( Equality inference for converse. $)
    cnveqi $p |- `' A = `' B $=
      ( wceq ccnv cnveq ax-mp ) ABDAEBEDCABFG $.
      $( [23-Dec-2008] $)
  $}

  ${
    cnveqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for converse. $)
    cnveqd $p |- ( ph -> `' A = `' B ) $=
      ( wceq ccnv cnveq syl ) ABCEBFCFEDBCGH $.
      $( [6-Dec-2013] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    $( Membership in a converse.  Equation 5 of [Suppes] p. 62. $)
    elcnv $p |- ( A e. `' R <-> E. x E. y ( A = <. x , y >. /\ y R x ) ) $=
      ( ccnv wcel cv wbr copab cop wceq wa wex df-cnv eleq2i elopab bitri ) CDE
      ZFCBGZAGZDHZABIZFCTSJKUALBMAMRUBCABDNOUAABCPQ $.
      $( [24-Mar-1998] $)

    $( Membership in a converse.  Equation 5 of [Suppes] p. 62. $)
    elcnv2 $p |- ( A e. `' R <->
                 E. x E. y ( A = <. x , y >. /\ <. y , x >. e. R ) ) $=
      ( ccnv wcel cv cop wceq wbr wa wex elcnv df-br anbi2i 2exbii bitri ) CDEF
      CAGZBGZHIZSRDJZKZBLALTSRHDFZKZBLALABCDMUBUDABUAUCTSRDNOPQ $.
      $( [11-Aug-2004] $)
  $}

  ${
    $d y z w A $.  $d x y z w $.
    hbcnv.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for converse. $)
    hbcnv $p |- ( y e. `' A -> A. x y e. `' A ) $=
      ( vz vw cv ccnv wcel cop wceq wbr wex elcnv ax-17 hbbr hban hbex hbxfrbi
      wa ) BGZCHIUAEGZFGZJKZUCUBCLZTZFMZEMAEFUACNUGAEUFAFUDUEAUDAOABUCUBCUAUCIA
      ODUAUBIAOPQRRS $.
      $( [31-Jan-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( Ordered-pair membership in converse.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    opelcnvg $p |- ( ( A e. C /\ B e. D ) ->
                     ( <. A , B >. e. `' R <-> <. B , A >. e. R ) ) $=
      ( vy vx wcel wa ccnv wbr cop cv breq2 breq1 df-cnv brabg df-br 3bitr3g )
      ACHBDHIABEJZKBAEKZABLTHBALEHFMZGMZEKUBAEKUAGFABCDTUCAUBENUBBAEOGFEPQABTRB
      AERS $.
      $( [27-Aug-2011] $) $( [13-May-1999] $)
  $}

  $( The converse of a binary relation swaps arguments.  Theorem 11 of [Suppes]
     p. 61. $)
  brcnvg $p |- ( ( A e. C /\ B e. D ) -> ( A `' R B <-> B R A ) ) $=
    ( wcel wa cop ccnv wbr opelcnvg df-br 3bitr4g ) ACFBDFGABHEIZFBAHEFABNJBAEJ
    ABCDEKABNLBAELM $.
    $( [10-Oct-2005] $)

  ${
    opelcnv.1 $e |- A e. _V $.
    opelcnv.2 $e |- B e. _V $.
    $( Ordered-pair membership in converse. $)
    opelcnv $p |- ( <. A , B >. e. `' R <-> <. B , A >. e. R ) $=
      ( cvv wcel cop ccnv wb opelcnvg mp2an ) AFGBFGABHCIGBAHCGJDEABFFCKL $.
      $( [13-Aug-1995] $)

    $( The converse of a binary relation swaps arguments.  Theorem 11 of
       [Suppes] p. 61. $)
    brcnv $p |- ( A `' R B <-> B R A ) $=
      ( cvv wcel ccnv wbr wb brcnvg mp2an ) AFGBFGABCHIBACIJDEABFFCKL $.
      $( [13-Aug-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Distributive law of converse over class composition.  Theorem 26 of
       [Suppes] p. 64.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    cnvco $p |- `' ( A o. B ) = ( `' B o. `' A ) $=
      ( vx vy vz cv ccom wbr copab ccnv wa wex exancom brco brcnv anbi12i exbii
      vex 3bitr4i opabbii df-cnv df-co 3eqtr4i ) CFZDFZABGZHZDCIUEEFZAJZHZUHUDB
      JZHZKZELZDCIUFJUKUIGUGUNDCUDUHBHZUHUEAHZKELUPUOKZELUGUNUOUPEMEUDUEABCRZDR
      ZNUMUQEUJUPULUOUEUHAUSERZOUHUDBUTUROPQSTDCUFUADCEUKUIUBUC $.
      $( [27-Aug-2011] $) $( [19-Mar-1998] $)
  $}

  ${
    $d x y z w A $.
    $( The converse of a class union is the (indexed) union of the converses of
       its members. $)
    cnvuni $p |- `' U. A = U_ x e. A `' x $=
      ( vy vz vw cuni ccnv cv ciun wcel wrex cop wa elcnv2 eluni2 anbi2i bitr4i
      wceq wex rexcom4 r19.42v 2exbii rexbii exbii 3bitrri 3bitri eliun eqriv )
      CBFZGZABAHZGZIZCHZUJJZUNULJZABKZUNUMJUOUNDHZEHZLRZUSURLZUIJZMZESDSUTVAUKJ
      ZMZABKZESZDSZUQDEUNUINVCVFDEVCUTVDABKZMVFVBVIUTAVABOPUTVDABUAQUBUQVEESZDS
      ZABKVJABKZDSVHUPVKABDEUNUKNUCVJADBTVLVGDVEAEBTUDUEUFAUNBULUGQUH $.
      $( [11-Aug-2004] $)
  $}

  ${
    $d x y A $.
    $( Alternate definition of domain.  Definition 6.5(1) of [TakeutiZaring]
       p. 24. $)
    dfdm3 $p |- dom A = { x | E. y <. x , y >. e. A } $=
      ( cdm cv wbr wex cab cop wcel df-dm df-br exbii abbii eqtri ) CDAEZBEZCFZ
      BGZAHPQICJZBGZAHABCKSUAARTBPQCLMNO $.
      $( [28-Dec-1996] $)

    $( Alternate definition of range.  Definition 4 of [Suppes] p. 60. $)
    dfrn2 $p |- ran A = { y | E. x x A y } $=
      ( crn ccnv cdm cv wbr wex cab df-rn df-dm vex brcnv exbii abbii 3eqtri )
      CDCEZFBGZAGZRHZAIZBJTSCHZAIZBJCKBARLUBUDBUAUCASTCBMAMNOPQ $.
      $( [27-Dec-1996] $)

    $( Alternate definition of range.  Definition 6.5(2) of [TakeutiZaring]
       p. 24. $)
    dfrn3 $p |- ran A = { y | E. x <. x , y >. e. A } $=
      ( crn cv wbr wex cab cop wcel dfrn2 df-br exbii abbii eqtri ) CDAEZBEZCFZ
      AGZBHPQICJZAGZBHABCKSUABRTAPQCLMNO $.
      $( [28-Dec-1996] $)
  $}

  ${
    $d x y A $.
    $( Alternate definition of domain. $)
    dfdm4 $p |- dom A = ran `' A $=
      ( vy vx cv wbr wex cab crn cdm vex brcnv exbii abbii dfrn2 df-dm 3eqtr4ri
      ccnv ) BDZCDZAQZEZBFZCGSRAEZBFZCGTHAIUBUDCUAUCBRSABJCJKLMBCTNCBAOP $.
      $( [28-Dec-1996] $)
  $}

  ${
    $d x y z w v $.  $d z w v A $.
    dfdmf.1 $e |- ( z e. A -> A. x z e. A ) $.
    dfdmf.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Definition of domain, using bound-variable hypotheses instead of
       distinct variable conditions. $)
    dfdmf $p |- dom A = { x | E. y <. x , y >. e. A } $=
      ( vw vv cdm cv cop wcel wex cab dfdm3 ax-17 hbel wceq opeq2 eleq1d exbidv
      cbvex abbii hbex opeq1 cbvab 3eqtri ) DIGJZHJZKZDLZHMZGNUHBJZKZDLZBMZGNAJ
      ZUMKZDLZBMZANGHDOULUPGUKUOHBBCCUJDCJZUJLBPFQUOHPUIUMRUJUNDUIUMUHSTUBUCUPU
      TGAUOABACCUNDVAUNLAPEQUDUTGPUHUQRZUOUSBVBUNURDUHUQUMUETUAUFUG $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    eldm.1 $e |- A e. _V $.
    $( Membership in a domain.  Theorem 4 of [Suppes] p. 59. $)
    eldm $p |- ( A e. dom B <-> E. y A B y ) $=
      ( vx cv wbr wex cdm wceq breq1 exbidv df-dm elab2 ) EFZAFZCGZAHBPCGZAHEBC
      IDOBJQRAOBPCKLEACMN $.
      $( [2-Apr-2004] $)

    $( Membership in a domain.  Theorem 4 of [Suppes] p. 59. $)
    eldm2 $p |- ( A e. dom B <-> E. y <. A , y >. e. B ) $=
      ( cdm wcel cv wbr wex cop eldm df-br exbii bitri ) BCEFBAGZCHZAIBOJCFZAIA
      BCDKPQABOCLMN $.
      $( [1-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Domain membership.  Theorem 4 of [Suppes] p. 59. $)
    eldm2g $p |- ( A e. V -> ( A e. dom B <-> E. y <. A , y >. e. B ) ) $=
      ( vx cv cdm wcel cop wex eleq1 wceq opeq1 eleq1d exbidv vex eldm2 vtoclbg
      ) EFZCGZHSAFZIZCHZAJBTHBUAIZCHZAJEBDSBTKSBLZUCUEAUFUBUDCSBUAMNOASCEPQR $.
      $( [27-Jan-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Subset theorem for domain. $)
    dmss $p |- ( A C_ B -> dom A C_ dom B ) $=
      ( vx vy wss cdm cv cop wcel wex ssel eximdv vex eldm2 3imtr4g ssrdv ) ABE
      ZCAFZBFZQCGZDGHZAIZDJUABIZDJTRITSIQUBUCDABUAKLDTACMZNDTBUDNOP $.
      $( [11-Aug-1994] $)
  $}

  $( Equality theorem for domain. $)
  dmeq $p |- ( A = B -> dom A = dom B ) $=
    ( wss wa cdm wceq dmss anim12i eqss 3imtr4i ) ABCZBACZDAEZBEZCZNMCZDABFMNFK
    OLPABGBAGHABIMNIJ $.
    $( [11-Aug-1994] $)

  ${
    dmeqi.1 $e |- A = B $.
    $( Equality inference for domain. $)
    dmeqi $p |- dom A = dom B $=
      ( wceq cdm dmeq ax-mp ) ABDAEBEDCABFG $.
      $( [4-Mar-2004] $)
  $}

  ${
    dmeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for domain. $)
    dmeqd $p |- ( ph -> dom A = dom B ) $=
      ( wceq cdm dmeq syl ) ABCEBFCFEDBCGH $.
      $( [4-Mar-2004] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y C $.
    opeldm.1 $e |- A e. _V $.
    $( Membership of first of an ordered pair in a domain. $)
    opeldm $p |- ( <. A , B >. e. C -> A e. dom C ) $=
      ( vy cvv wcel cop cdm wi cv wex opeq2 eleq1d cla4egv eldm2 syl6ibr opprc2
      wceq wn cla4ev sylibr syl6bi pm2.61i ) BFGZABHZCGZACIGZJUEUGAEKZHZCGZELZU
      HUKUGEBFUIBSUJUFCUIBAMNOEACDPZQUETZUGAAHZCGZUHUNUFUOCABRNUPULUHUKUPEADUIA
      SUJUOCUIAAMNUAUMUBUCUD $.
      $( [30-Jul-1995] $)
  $}

  ${
    breldm.1 $e |- A e. _V $.
    $( Membership of first of a binary relation in a domain. $)
    breldm $p |- ( A R B -> A e. dom R ) $=
      ( wbr cop wcel cdm df-br opeldm sylbi ) ABCEABFCGACHGABCIABCDJK $.
      $( [30-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x R $.
    $( Membership of first of a binary relation in a domain. $)
    breldmg $p |- ( ( A e. C /\ A R B ) -> A e. dom R ) $=
      ( vx wcel wbr cdm cv wi wceq breq1 eleq1 imbi12d vex breldm vtoclg imp )
      ACFABDGZADHZFZEIZBDGZUBTFZJSUAJEACUBAKUCSUDUAUBABDLUBATMNUBBDEOPQR $.
      $( [21-Mar-2007] $)

    $( The domain of a union is the union of domains.  Exercise 56(a) of
       [Enderton] p. 65.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    dmun $p |- dom ( A u. B ) = ( dom A u. dom B ) $=
      ( vy vx cv wbr wex cab cun cdm wo unab brun exbii 19.43 abbii eqtri df-dm
      bitr2i uneq12i 3eqtr4ri ) CEZDEZAFZDGZCHZUBUCBFZDGZCHZIZUBUCABIZFZDGZCHZA
      JZBJZIUKJUJUEUHKZCHUNUEUHCLUQUMCUMUDUGKZDGUQULURDUBUCABMNUDUGDOSPQUOUFUPU
      ICDARCDBRTCDUKRUA $.
      $( [27-Aug-2011] $) $( [12-Aug-1994] $)

    $( The domain of an intersection belong to the intersection of domains.
       Theorem 6 of [Suppes] p. 60. $)
    dmin $p |- dom ( A i^i B ) C_ ( dom A i^i dom B ) $=
      ( vx vy cin cdm cv cop wcel wa wex 19.40 eldm2 elin exbii anbi12i 3imtr4i
      vex bitri ssriv ) CABEZFZAFZBFZEZCGZDGHZAIZUGBIZJZDKZUHDKZUIDKZJZUFUBIZUF
      UEIZUHUIDLUOUGUAIZDKUKDUFUACRZMUQUJDUGABNOSUPUFUCIZUFUDIZJUNUFUCUDNUSULUT
      UMDUFAURMDUFBURMPSQT $.
      $( [15-Sep-2004] $)

  $}

  ${
    $d x y z A $.
    $( The domain of a union.  Part of Exercise 8 of [Enderton] p. 41. $)
    dmuni $p |- dom U. A = U_ x e. A dom x $=
      ( vy vz cuni cdm cv ciun cop wcel wex wrex eluni exbii excom ancom 19.41v
      wa eldm2 3bitr4i vex anbi2i 3bitri df-rex bitr4i eliun eqriv ) CBEZFZABAG
      ZFZHZCGZDGIZUHJZDKZUMUKJZABLZUMUIJUMULJUPUJBJZUQRZAKZURUPUNUJJZUSRZAKZDKV
      CDKZAKVAUOVDDAUNBMNVCDAOVEUTAVBDKZUSRUSVFRVEUTVFUSPVBUSDQUQVFUSDUMUJCUAZS
      UBTNUCUQABUDUEDUMUHVGSAUMBUKUFTUG $.
      $( [3-Feb-2004] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( The domain of a class of ordered pairs. $)
    dmopab $p |- dom { <. x , y >. | ph } = { x | E. y ph } $=
      ( vz copab cdm cv cop wcel wex cab hbopab1 dfdmf opabid exbii abbii eqtri
      hbopab2 ) ABCEZFBGCGHSIZCJZBKACJZBKBCDSABCDLABCDRMUAUBBTACABCNOPQ $.
      $( [16-May-1995] $)
  $}

  ${
    $d x y A $.
    $( Upper bound for the domain of a restricted class of ordered pairs. $)
    dmopabss $p |- dom { <. x , y >. | ( x e. A /\ ph ) } C_ A $=
      ( cv wcel wa copab cdm wex cab dmopab 19.42v abbii ssab2 eqsstri ) BEDFZA
      GZBCHIRCJZBKZDRBCLTQACJZGZBKDSUBBQACMNUABDOPP $.
      $( [31-Jan-2004] $)
  $}

  ${
    $d x y A $.
    $( The domain of a restricted class of ordered pairs. $)
    dmopab3 $p |- ( A. x e. A E. y ph <->
               dom { <. x , y >. | ( x e. A /\ ph ) } = A ) $=
      ( wex wral cv wcel wi wal wa wb copab cdm wceq df-ral pm4.71 albii dmopab
      cab 19.42v abbii eqtri eqeq1i eqcom abeq2 3bitr2ri 3bitri ) ACEZBDFBGDHZU
      IIZBJUJUJUIKZLZBJZUJAKZBCMNZDOZUIBDPUKUMBUJUIQRUQULBTZDODUROUNUPURDUPUOCE
      ZBTURUOBCSUSULBUJACUAUBUCUDDURUEULBDUFUGUH $.
      $( [31-Jan-2004] $)
  $}

  ${
    $d x y $.
    $( The domain of the empty set is empty.  Part of Theorem 3.8(v) of [Monk1]
       p. 36.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    dm0 $p |- dom (/) = (/) $=
      ( vx vy c0 cdm wceq cv wcel eq0 cop wex noel nex vex eldm2 mtbir mpgbir
      wn ) CDZCEAFZRGZQAARHTSBFIZCGZBJUBBUAKLBSCAMNOP $.
      $( [27-Aug-2011] $) $( [4-Jul-1994] $)

    $( The domain of the identity relation is the universe.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    dmi $p |- dom _I = _V $=
      ( vx vy cid cdm cvv wceq wcel eqv wbr wex a9e vex ideq equcom bitri exbii
      cv mpbir eldm mpgbir ) CDZEFAQZUAGZAAUAHUCUBBQZCIZBJZUFUDUBFZBJBAKUEUGBUE
      UBUDFUGUBUDBLMABNOPRBUBCALSRT $.
      $( [27-Aug-2011] $) $( [30-Apr-1998] $)

    $( The domain of the universe is the universe. $)
    dmv $p |- dom _V = _V $=
      ( cvv cdm ssv cid dmi wss dmss ax-mp eqsstr3i eqssi ) ABZAKCADBZKEDAFLKFD
      CDAGHIJ $.
      $( [8-Aug-2003] $)
  $}

  ${
    $d x y A $.
    $( An empty domain implies an empty range. $)
    dm0rn0 $p |- ( dom A = (/) <-> ran A = (/) ) $=
      ( vx vy cv wbr wex cab c0 wceq wcel wb wal alnex 3bitr4i noel albii abeq1
      wn nbn eqeq1i cdm crn excom notbii 3bitr3i df-dm dfrn2 ) BDZCDZAEZCFZBGZH
      IZUJBFZCGZHIZAUAZHIAUBZHIUKUHHJZKZBLZUNUIHJZKZCLZUMUPUKRZBLZUNRZCLZVAVDUK
      BFZRUNCFZRVFVHVIVJUJBCUCUDUKBMUNCMNVEUTBUSUKUHOSPVGVCCVBUNUIOSPUEUKBHQUNC
      HQNUQULHBCAUFTURUOHBCAUGTN $.
      $( [21-May-1998] $)

    $( A relation is empty iff its domain is empty. $)
    reldm0 $p |- ( Rel A -> ( A = (/) <-> dom A = (/) ) ) $=
      ( vx vy wrel c0 wceq cv cop wcel wb wal cdm rel0 eqrel mpan2 wn eq0 albii
      wex vex eldm2 notbii alnex noel nbn 3bitr2i bitr2i syl6bb ) ADZAEFZBGZCGH
      ZAIZULEIZJZCKZBKZALZEFZUIEDUJUQJMBCAENOUSUKURIZPZBKUQBURQVAUPBVAUMCSZPUMP
      ZCKUPUTVBCUKABTUAUBUMCUCVCUOCUNUMULUDUERUFRUGUH $.
      $( [15-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The domain of a cross product.  Part of Theorem 3.13(x) of [Monk1]
       p. 37.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    dmxp $p |- ( B =/= (/) -> dom ( A X. B ) = A ) $=
      ( vy vx c0 wne cxp cdm cv wcel copab df-xp dmeqi wex wral wceq biimpi a1d
      wa n0 ralrimiv dmopab3 sylib syl5eq ) BEFZABGZHCIAJZDIBJZSCDKZHZAUFUICDAB
      LMUEUHDNZCAOUJAPUEUKCAUEUKUGUEUKDBTQRUAUHCDAUBUCUD $.
      $( [27-Aug-2011] $) $( [28-Jul-1995] $)
  $}

  $( The domain of a square cross product. $)
  dmxpid $p |- dom ( A X. A ) = A $=
    ( cxp cdm wceq c0 dm0 xpeq1 xp0r syl6eq dmeqd id 3eqtr4a dmxp pm2.61ine ) A
    ABZCZADAEAEDZECEPAFQOEQOEABEAEAGAHIJQKLAAMN $.
    $( [28-Jul-1995] $)

  $( The domain of the intersection of two square cross products.  Unlike
     ~ dmin , equality holds. $)
  dmxpin $p |- dom ( ( A X. A ) i^i ( B X. B ) ) = ( A i^i B ) $=
    ( cxp cin cdm inxp dmeqi dmxpid eqtri ) AACBBCDZEABDZKCZEKJLAABBFGKHI $.
    $( [29-Jan-2008] $)

  $( The cross product of a class with itself is one-to-one.  (The proof was
     shortened by Andrew Salmon, 27-Aug-2011.) $)
  xpid11 $p |- ( ( A X. A ) = ( B X. B ) <-> A = B ) $=
    ( cxp wceq cdm dmeq dmxpid 3eqtr3g xpeq12 anidms impbii ) AACZBBCZDZABDZNLE
    MEABLMFAGBGHONABABIJK $.
    $( [27-Aug-2011] $) $( [5-Nov-2006] $)

  $( The domain of the double converse of a class (which doesn't have to be a
     relation as in ~ dfrel2 ). $)
  dmcnvcnv $p |- dom `' `' A = dom A $=
    ( cdm ccnv crn dfdm4 df-rn eqtr2i ) ABACZDHCBAEHFG $.
    $( [8-Apr-2007] $)

  $( The range of the double converse of a class. $)
  rncnvcnv $p |- ran `' `' A = ran A $=
    ( crn ccnv cdm df-rn dfdm4 eqtr2i ) ABACZDHCBAEHFG $.
    $( [8-Apr-2007] $)

  ${
    $d x y A $.  $d x y B $.
    $( The first member of an ordered pair in a relation belongs to the domain
       of the relation. $)
    elreldm $p |- ( ( Rel A /\ B e. A ) -> |^| |^| B e. dom A ) $=
      ( vx vy wrel wcel cint cdm cv cop wceq wex cvv cxp wss wi ssel sylbi elvv
      df-rel syl6ib eleq1 vex opeldm syl6bi inteqd op1stb syl6eq eleq1d sylibrd
      inteq exlimivv syli imp ) AEZBAFZBGZGZAHZFZUPUOBCIZDIZJZKZDLCLZUTUOUPBMMN
      ZFZVEUOAVFOUPVGPATAVFBQRCDBSUAVDUPUTPCDVDUPVAUSFZUTVDUPVCAFVHBVCAUBVAVBAC
      UCZUDUEVDURVAUSVDURVCGZGVAVDUQVJBVCUKUFVAVBVIUGUHUIUJULUMUN $.
      $( [28-Jul-2004] $)
  $}

  $( Equality theorem for range. $)
  rneq $p |- ( A = B -> ran A = ran B ) $=
    ( wceq ccnv cdm crn cnveq dmeqd df-rn 3eqtr4g ) ABCZADZEBDZEAFBFKLMABGHAIBI
    J $.
    $( [29-Dec-1996] $)

  ${
    rneqi.1 $e |- A = B $.
    $( Equality inference for range. $)
    rneqi $p |- ran A = ran B $=
      ( wceq crn rneq ax-mp ) ABDAEBEDCABFG $.
      $( [4-Mar-2004] $)
  $}

  ${
    rneqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for range. $)
    rneqd $p |- ( ph -> ran A = ran B ) $=
      ( wceq crn rneq syl ) ABCEBFCFEDBCGH $.
      $( [4-Mar-2004] $)
  $}

  $( Subset theorem for range. $)
  rnss $p |- ( A C_ B -> ran A C_ ran B ) $=
    ( wss ccnv cdm crn cnvss dmss syl df-rn 3sstr4g ) ABCZADZEZBDZEZAFBFLMOCNPC
    ABGMOHIAJBJK $.
    $( [22-Mar-1998] $)

  $( The second argument of a binary relation belongs to its range. $)
  brelrng $p |- ( ( A e. F /\ B e. G /\ A C B ) -> B e. ran C ) $=
    ( wcel wbr w3a ccnv cdm crn brcnvg ancoms biimp3ar breldmg 3adant1 syld3an3
    wb df-rn syl6eleqr ) ADFZBEFZABCGZHBCIZJZCKUAUBUCBAUDGZBUEFZUAUBUFUCUBUAUFU
    CRBAEDCLMNUBUFUGUABAEUDOPQCST $.
    $( [29-Jun-2008] $)

  ${
    brelrn.1 $e |- A e. _V $.
    brelrn.2 $e |- B e. _V $.
    $( The second argument of a binary relation belongs to its range. $)
    brelrn $p |- ( A C B -> B e. ran C ) $=
      ( cvv wcel wbr crn brelrng mp3an1 mpan ) BFGZABCHZBCIGZEAFGMNODABCFFJKL
      $.
      $( [29-Jun-2008] $) $( [13-Aug-2004] $)

    $( Membership of second member of an ordered pair in a range. $)
    opelrn $p |- ( <. A , B >. e. C -> B e. ran C ) $=
      ( cop wcel wbr crn df-br brelrn sylbir ) ABFCGABCHBCIGABCJABCDEKL $.
      $( [23-Feb-1997] $)
  $}

  $( The first argument of a binary relation belongs to its domain. $)
  releldm $p |- ( ( Rel R /\ A R B ) -> A e. dom R ) $=
    ( wrel wbr cvv wcel cdm brrelex breldmg sylancom ) CDABCEAFGACHGABCIABFCJK
    $.
    $( [2-Jul-2008] $)

  $( The second argument of a binary relation belongs to its range. $)
  relelrng $p |- ( ( B e. C /\ Rel R /\ A R B ) -> B e. ran R ) $=
    ( wcel cvv wrel wbr crn brrelex 3adant1 brelrng 3com12 syld3an2 ) BCEZAFEZD
    GZABDHZBDIEZQRPOABDJKPORSABDFCLMN $.
    $( [2-Jul-2008] $)

  ${
    $d x y z w v $.  $d z w v A $.
    dfrnf.1 $e |- ( z e. A -> A. x z e. A ) $.
    dfrnf.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Definition of range, using bound-variable hypotheses instead of distinct
       variable conditions. $)
    dfrnf $p |- ran A = { y | E. x <. x , y >. e. A } $=
      ( vv vw crn cv cop wcel wex cab dfrn3 ax-17 hbel wceq opeq1 eleq1d exbidv
      cbvex abbii hbex opeq2 cbvab 3eqtri ) DIGJZHJZKZDLZGMZHNAJZUIKZDLZAMZHNUM
      BJZKZDLZAMZBNGHDOULUPHUKUOGAACCUJDCJZUJLAPEQUOGPUHUMRUJUNDUHUMUISTUBUCUPU
      THBUOBABCCUNDVAUNLBPFQUDUTHPUIUQRZUOUSAVBUNURDUIUQUMUETUAUFUG $.
      $( [14-Aug-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    elrn.1 $e |- A e. _V $.
    $( Membership in a range. $)
    elrn2 $p |- ( A e. ran B <-> E. x <. x , A >. e. B ) $=
      ( vy cv cop wcel wex crn wceq opeq2 eleq1d exbidv dfrn3 elab2 ) AFZEFZGZC
      HZAIQBGZCHZAIEBCJDRBKZTUBAUCSUACRBQLMNAECOP $.
      $( [10-Jul-1994] $)

    $( Membership in a range. $)
    elrn $p |- ( A e. ran B <-> E. x x B A ) $=
      ( crn wcel cv cop wex wbr elrn2 df-br exbii bitr4i ) BCEFAGZBHCFZAIOBCJZA
      IABCDKQPAOBCLMN $.
      $( [2-Apr-2004] $)
  $}

  ${
    $d x y z w $.  $d y z w A $.
    hbrn.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for range.  (The proof was shortened
       by Andrew Salmon, 27-Aug-2011.) $)
    hbrn $p |- ( y e. ran A -> A. x y e. ran A ) $=
      ( vz vw cv crn wcel cop wex vex elrn2 ax-17 hbel hbex hbxfrbi ) BGZCHIEGR
      JZCIZEKAERCBLMTAEAFBSCFGSIANDOPQ $.
      $( [27-Aug-2011] $) $( [1-Sep-1999] $)
  $}

  ${
    $d x y z w $.  $d y z w A $.
    hbdm.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for domain. $)
    hbdm $p |- ( y e. dom A -> A. x y e. dom A ) $=
      ( cdm ccnv crn dfdm4 hbcnv hbrn hbxfreq ) ABCECFZGCHABLABCDIJK $.
      $( [30-Jan-2004] $)
  $}

  ${
    $d A z $.  $d B z $.  $d x z $.
    $( Domain of an intersection.  (Contributed by FL, 15-Oct-2012.) $)
    dmiin $p |- dom |^|_ x e. A B C_ |^|_ x e. A dom B $=
      ( vz ciin cdm wss hbii1 hbdm ssiinf cv wcel iinss2 dmss syl mprgbir ) ABC
      EZFZABCFZEGRSGZABADBSRADQADBCHIJAKBLQCGTABCMQCNOP $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d A w y $.  $d B w y $.  $d V w y $.  $d x w y $.
    $( Distribute proper substitution through the range of a class. ~ csbrng is
       derived from the virtual deduction proof ~ csbrngVD .  (Contributed by
       Alan Sare, 10-Nov-2012. ) $)
    csbrng $p |- ( A e. V -> [_ A / x ]_ ran B = ran [_ A / x ]_ B ) $=
      ( vw vy wcel crn csb cop wex cab wsbc dfrn3 csbeq2i csbabg sbcexg sbcel2g
      cv exbidv bitrd abbidv 3eqtrd syl6eqr ) BDGZABCHZIZESFSJZABCIZGZEKZFLZUIH
      UEUGABUHCGZEKZFLZIUNABMZFLULABUFUODEFCNOUNAFBDPUEUPUKFUEUPUMABMZEKUKUMEAB
      DQUEUQUJEABUHCDRTUAUBUCEFUINUD $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( The range of a class of ordered pairs. $)
    rnopab $p |- ran { <. x , y >. | ph } = { y | E. x ph } $=
      ( vz copab crn cv cop wcel wex cab hbopab1 dfrnf opabid exbii abbii eqtri
      hbopab2 ) ABCEZFBGCGHSIZBJZCKABJZCKBCDSABCDLABCDRMUAUBCTABABCNOPQ $.
      $( [14-Aug-1995] $)
  $}

  ${
    $d x y $.
    $( The range of a function expressed as a class abstraction. $)
    rnopab2 $p |- ran { <. x , y >. | ( x e. A /\ y = B ) } =
                 { y | E. x e. A y = B } $=
      ( cv wcel wceq wa copab crn wex cab wrex rnopab df-rex abbii eqtr4i ) AEC
      FBEDGZHZABIJSAKZBLRACMZBLSABNUATBRACOPQ $.
      $( [23-Mar-2006] $)
  $}

  $( The range of the empty set is empty.  Part of Theorem 3.8(v) of [Monk1]
     p. 36. $)
  rn0 $p |- ran (/) = (/) $=
    ( c0 cdm wceq crn dm0 dm0rn0 mpbi ) ABACADACEAFG $.
    $( [4-Jul-1994] $)

  ${
    $d x y A $.
    $( A relation is empty iff its range is empty. $)
    relrn0 $p |- ( Rel A -> ( A = (/) <-> ran A = (/) ) ) $=
      ( wrel c0 wceq cdm crn reldm0 dm0rn0 syl6bb ) ABACDAECDAFCDAGAHI $.
      $( [15-Sep-2004] $)

    $( The domain and range of a class are included in its double union. $)
    dmrnssfld $p |- ( dom A u. ran A ) C_ U. U. A $=
      ( vx vy cdm crn cuni cv cop wex vex eldm2 cpr prid1 wss sseld mpi exlimiv
      wcel sylbi ssriv uniop uniopel syl5eqelr elssuni syl elrn2 prid2 unssi )
      ADZAEZAFZFZBUIULBGZUIRUMCGZHZARZCIUMULRZCUMABJZKUPUQCUPUMUMUNLZRUQUMUNURM
      UPUSULUMUPUSUKRUSULNUPUSUOFUKUMUNUAUMUNAUBUCUSUKUDUEZOPQSTCUJULUNUJRUPBIU
      NULRZBUNACJZUFUPVABUPUNUSRVAUMUNVBUGUPUSULUNUTOPQSTUH $.
      $( [13-May-2008] $)
  $}

  $( The domain of a set is a set.  Corollary 6.8(2) of [TakeutiZaring]
     p. 26. $)
  dmexg $p |- ( A e. V -> dom A e. _V ) $=
    ( wcel cuni cvv cdm uniexg wss crn ssun1 dmrnssfld sstri ssexg mpan 3syl
    cun ) ABCADZECQDZECZAFZECZABGQEGTRHSUATTAIZPRTUBJAKLTREMNO $.
    $( [7-Apr-1995] $)

  $( The range of a set is a set.  Corollary 6.8(3) of [TakeutiZaring] p. 26.
     Similar to Lemma 3D of [Enderton] p. 41. $)
  rnexg $p |- ( A e. V -> ran A e. _V ) $=
    ( wcel cuni cvv crn uniexg wss cdm ssun2 dmrnssfld sstri ssexg mpan 3syl
    cun ) ABCADZECQDZECZAFZECZABGQEGTRHSUATAIZTPRTUBJAKLTREMNO $.
    $( [31-Mar-1995] $)

  ${
    dmex.1 $e |- A e. _V $.
    $( The domain of a set is a set.  Corollary 6.8(2) of [TakeutiZaring]
       p. 26. $)
    dmex $p |- dom A e. _V $=
      ( cvv wcel cdm dmexg ax-mp ) ACDAECDBACFG $.
      $( [7-Jul-2008] $)

    $( The range of a set is a set.  Corollary 6.8(3) of [TakeutiZaring]
       p. 26.  Similar to Lemma 3D of [Enderton] p. 41. $)
    rnex $p |- ran A e. _V $=
      ( cvv wcel crn rnexg ax-mp ) ACDAECDBACFG $.
      $( [7-Jul-2008] $)
  $}

  $( The identity function is a proper class.  This means, for example, that we
     cannot use it as a member of the class of continuous functions unless it
     is restricted to a set, as in ~ idcn . $)
  iprc $p |- -. _I e. _V $=
    ( cid cvv wcel cdm vprc dmi eleq1i mtbir dmexg mto ) ABCADZBCZLBBCEKBBFGHAB
    IJ $.
    $( [1-Jan-2007] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( Domain of a composition.  Theorem 21 of [Suppes] p. 63.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    dmcoss $p |- dom ( A o. B ) C_ dom B $=
      ( vx vy vz ccom cdm cv cop wcel wex wbr hbe1 wa exsimpl vex opelco cbvexv
      breq2 3imtr4i exlimi eldm2 eldm ssriv ) CABFZGZBGZCHZDHZIUEJZDKUHUIBLZDKZ
      UHUFJUHUGJUJULDUKDMUHEHZBLZUMUIALZNEKUNEKUJULUNUOEOEUHUIABCPZDPQUKUNDEUIU
      MUHBSRTUADUHUEUPUBDUHBUPUCTUD $.
      $( [27-Aug-2011] $) $( [19-Mar-1998] $)
  $}

  $( Range of a composition. $)
  rncoss $p |- ran ( A o. B ) C_ ran A $=
    ( ccnv ccom cdm crn dmcoss df-rn cnvco dmeqi eqtri 3sstr4i ) BCZACZDZEZNEAB
    DZFZAFMNGRQCZEPQHSOABIJKAHL $.
    $( [19-Mar-1998] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( Domain of a composition.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    dmcosseq $p |- ( ran B C_ dom A -> dom ( A o. B ) = dom B ) $=
      ( vx vy vz crn cdm wss ccom dmcoss a1i cv wbr wex wcel wi vex eldm eximdv
      syl6ibr wa ssel elrn imbi12i 19.8a imim1i pm3.2 sylcom sylbi excom opelco
      cop syl exbii eldm2 3imtr4g ssrdv eqssd ) BFZAGZHZABIZGZBGZVCVDHVAABJKVAC
      VDVCVACLZDLZBMZDNZVEELZULVBOZENZVEVDOVEVCOVAVHVGVFVIAMZUAZDNZENZVKVAVHVME
      NZDNVOVAVGVPDVAVFUSOZVFUTOZPZVGVPPZUSUTVFUBVSVGCNZVLENZPZVTVQWAVRWBCVFBDQ
      ZUCEVFAWDRUDWCVGWBVPVGWAWBVGCUEUFVGVLVMEVGVLUGSUHUIUMSVMEDUJTVJVNEDVEVIAB
      CQZEQUKUNTDVEBWEREVEVBWEUOUPUQUR $.
      $( [27-Aug-2011] $) $( [28-May-1998] $)

    $( Domain of a composition. $)
    dmcoeq $p |- ( dom A = ran B -> dom ( A o. B ) = dom B ) $=
      ( cdm crn wceq wss ccom eqimss2 dmcosseq syl ) ACZBDZELKFABGCBCELKHABIJ
      $.
      $( [19-Mar-1998] $)
  $}

  $( Range of a composition. $)
  rncoeq $p |- ( dom A = ran B -> ran ( A o. B ) = ran A ) $=
    ( ccnv cdm crn wceq ccom dmcoeq eqcom df-rn dfdm4 eqeq12i bitri cnvco dmeqi
    eqtri 3imtr4i ) BCZDZACZEZFZRTGZDZTDZFADZBEZFZABGZEZAEZFRTHUHUGUFFUBUFUGIUG
    SUFUABJAKLMUJUDUKUEUJUICZDUDUIJULUCABNOPAJLQ $.
    $( [19-Mar-1998] $)

  $( Equality theorem for restrictions. $)
  reseq1 $p |- ( A = B -> ( A |` C ) = ( B |` C ) ) $=
    ( wceq cvv cxp cin cres ineq1 df-res 3eqtr4g ) ABDACEFZGBLGACHBCHABLIACJBCJ
    K $.
    $( [7-Aug-1994] $)

  $( Equality theorem for restrictions. $)
  reseq2 $p |- ( A = B -> ( C |` A ) = ( C |` B ) ) $=
    ( wceq cvv cxp cin cres xpeq1 ineq2d df-res 3eqtr4g ) ABDZCAEFZGCBEFZGCAHCB
    HMNOCABEIJCAKCBKL $.
    $( [8-Aug-1994] $)

  ${
    reseq2i.1 $e |- A = B $.
    $( Equality inference for restrictions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    reseq2i $p |- ( F |` A ) = ( F |` B ) $=
      ( wceq cres reseq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [22-Jun-2011] $)
  $}

  ${
    reseq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for restrictions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    reseq2d $p |- ( ph -> ( F |` A ) = ( F |` B ) ) $=
      ( wceq cres reseq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d y B $.  $d x y $.
    hbres.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbres.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for restriction.  (Unnecessary
       distinct variable restrictions were removed by David Abernethy,
       19-Jun-2012.) $)
    hbres $p |- ( y e. ( A |` B ) -> A. x y e. ( A |` B ) ) $=
      ( cres cvv cxp cin df-res cv wcel ax-17 hbxp hbin hbxfreq ) ABCDGCDHIZJCD
      KABCREABDHFBLHMANOPQ $.
      $( [24-Jul-2012] $) $( [15-Sep-2003] $)
  $}

  $( Distribute proper substitution through the restriction of a class.
     ~ csbresg is derived from the virtual deduction proof ~ csbresgVD .
     (Contributed by Alan Sare, 10-Nov-2012. ) $)
  csbresg $p |- ( A e. V -> [_ A / x ]_ ( B |` C ) =
                 ( [_ A / x ]_ B |` [_ A / x ]_ C ) ) $=
    ( wcel cres csb cvv cxp df-res csbeq2i csbing csbxpg wceq csbconstgfv xpeq2
    cin syl eqtrd ineq2d 3eqtrd syl6eqr ) BEFZABCDGZHZABCHZABDHZIJZRZUGUHGUDUFA
    BCDIJZRZHUGABUKHZRUJABUEULECDKLABECUKMUDUMUIUGUDUMUHABIHZJZUIABDIENUDUNIOUO
    UIOABIEPUNIUHQSTUAUBUGUHKUC $.
    $( [10-Nov-2012] $)

  $( A restriction to the empty set is empty. $)
  res0 $p |- ( A |` (/) ) = (/) $=
    ( c0 cres cvv cxp cin df-res xp0r ineq2i in0 3eqtri ) ABCABDEZFABFBABGLBADH
    IAJK $.
    $( [12-Nov-1994] $)

  ${
    opelres.1 $e |- B e. _V $.
    $( Ordered pair membership in a restriction.  Exercise 13 of
       [TakeutiZaring] p. 25. $)
    opelres $p |- ( <. A , B >. e. ( C |` D ) <->
                    ( <. A , B >. e. C /\ A e. D ) ) $=
      ( cop cres wcel cvv cxp cin wa df-res eleq2i elin opelxp mpbiran2 anbi2i
      3bitri ) ABFZCDGZHTCDIJZKZHTCHZTUBHZLUDADHZLUAUCTCDMNTCUBOUEUFUDUEUFBIHAB
      DIEPEQRS $.
      $( [13-Nov-1995] $)

    $( Binary relation on a restriction. $)
    brres $p |- ( A ( C |` D ) B <-> ( A C B /\ A e. D ) ) $=
      ( cop cres wcel wa wbr opelres df-br anbi1i 3bitr4i ) ABFZCDGZHOCHZADHZIA
      BPJABCJZRIABCDEKABPLSQRABCLMN $.
      $( [12-Dec-2006] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y C $.  $d y D $.
    $( Ordered pair membership in a restriction.  Exercise 13 of
       [TakeutiZaring] p. 25. $)
    opelresg $p |- ( B e. V -> ( <. A , B >. e. ( C |` D ) <->
                    ( <. A , B >. e. C /\ A e. D ) ) ) $=
      ( vy cv cop cres wcel wa wceq opeq2 eleq1d anbi1d vex opelres vtoclbg ) A
      FGZHZCDIZJTCJZADJZKABHZUAJUDCJZUCKFBESBLZTUDUASBAMZNUFUBUEUCUFTUDCUGNOASC
      DFPQR $.
      $( [14-Oct-2005] $)
  $}

  ${
    opres.1 $e |- B e. _V $.
    $( Ordered pair membership in a restriction when the first member belongs
       to the restricting class.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    opres $p |- ( A e. D ->
                    ( <. A , B >. e. ( C |` D ) <-> <. A , B >. e. C ) ) $=
      ( cop cres wcel wa opelres ancom bitri baib ) ABFZCDGHZADHZNCHZOQPIPQIABC
      DEJQPKLM $.
      $( [27-Aug-2011] $) $( [30-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( A restricted identity relation is equivalent to equality in its
       domain. $)
    resieq $p |- ( ( B e. A /\ C e. A ) -> ( B ( _I |` A ) C <-> B = C ) ) $=
      ( vx wcel cid cres wbr wceq wb cv wi breq2 eqeq2 bibi12d imbi2d cop opres
      vex df-br ideq bitr3i 3bitr4g vtoclg impcom ) CAEBAEZBCFAGZHZBCIZJZUFBDKZ
      UGHZBUKIZJZLUFUJLDCAUKCIZUNUJUFUOULUHUMUIUKCBUGMUKCBNOPUFBUKQZUGEUPFEZULU
      MBUKFADSZRBUKUGTUMBUKFHUQBUKURUABUKFTUBUCUDUE $.
      $( [30-Apr-2004] $)
  $}

  $( The restriction of a restriction. $)
  resres $p |- ( ( A |` B ) |` C ) = ( A |` ( B i^i C ) ) $=
    ( cres cvv cxp cin df-res ineq1i xpindir ineq2i inass 3eqtr4ri 3eqtri ) ABD
    ZCDOCEFZGABEFZGZPGZABCGZDZOCHORPABHIATEFZGAQPGZGUASUBUCABCEJKATHAQPLMN $.
    $( [27-Mar-2008] $)

  $( Distributive law for restriction over union.  Theorem 31 of [Suppes]
     p. 65. $)
  resundi $p |- ( A |` ( B u. C ) ) = ( ( A |` B ) u. ( A |` C ) ) $=
    ( cun cvv cxp cin cres xpundir ineq2i indi eqtri df-res uneq12i 3eqtr4i ) A
    BCDZEFZGZABEFZGZACEFZGZDZAPHABHZACHZDRASUADZGUCQUFABCEIJASUAKLAPMUDTUEUBABM
    ACMNO $.
    $( [30-Sep-2002] $)

  $( Distributive law for restriction over union. $)
  resundir $p |- ( ( A u. B ) |` C ) = ( ( A |` C ) u. ( B |` C ) ) $=
    ( cun cvv cxp cin cres indir df-res uneq12i 3eqtr4i ) ABDZCEFZGANGZBNGZDMCH
    ACHZBCHZDABNIMCJQORPACJBCJKL $.
    $( [23-Sep-2004] $)

  $( Class restriction distributes over intersection.  (Contributed by FL,
     6-Oct-2008.) $)
  resindi $p |- ( A |` ( B i^i C ) ) = ( ( A |` B ) i^i ( A |` C ) ) $=
    ( cin cvv cxp cres xpindir ineq2i inindi eqtri df-res ineq12i 3eqtr4i ) ABC
    DZEFZDZABEFZDZACEFZDZDZAOGABGZACGZDQARTDZDUBPUEABCEHIARTJKAOLUCSUDUAABLACLM
    N $.
    $( [6-Oct-2008] $)

  $( Class restriction distributes over intersection. $)
  resindir $p |- ( ( A i^i B ) |` C ) = ( ( A |` C ) i^i ( B |` C ) ) $=
    ( cin cvv cxp cres inindir df-res ineq12i 3eqtr4i ) ABDZCEFZDAMDZBMDZDLCGAC
    GZBCGZDABMHLCIPNQOACIBCIJK $.
    $( [18-Dec-2008] $)

  $( Move intersection into class restriction. $)
  inres $p |- ( A i^i ( B |` C ) ) = ( ( A i^i B ) |` C ) $=
    ( cin cvv cxp cres inass df-res ineq2i 3eqtr4ri ) ABDZCEFZDABMDZDLCGABCGZDA
    BMHLCIONABCIJK $.
    $( [18-Dec-2008] $)

  ${
    $d x y A $.  $d x y B $.
    $( The domain of a restriction.  Exercise 14 of [TakeutiZaring] p. 25. $)
    dmres $p |- dom ( A |` B ) = ( B i^i dom A ) $=
      ( vx vy cdm cin cres cv cop wcel wex wa opelres exbii eldm2 anbi1i 19.41v
      vex bitr4i 3bitr4ri ineqri incom eqtr3i ) AEZBFABGZEZBUDFCUDBUFCHZDHZIZUE
      JZDKUIAJZUGBJZLZDKZUGUFJUGUDJZULLZUJUMDUGUHABDRMNDUGUECRZOUPUKDKZULLUNUOU
      RULDUGAUQOPUKULDQSTUAUDBUBUC $.
      $( [1-Aug-1994] $)
  $}

  $( A domain restricted to a subclass equals the subclass. $)
  ssdmres $p |- ( A C_ dom B <-> dom ( B |` A ) = A ) $=
    ( cdm wss cin wceq cres df-ss dmres eqeq1i bitr4i ) ABCZDALEZAFBAGCZAFALHNM
    ABAIJK $.
    $( [28-Aug-2004] $) $( [2-Mar-1997] $)

  $( The domain of a restriction to a set exists. $)
  dmresexg $p |- ( B e. V -> dom ( A |` B ) e. _V ) $=
    ( wcel cres cdm cin cvv dmres inex1g syl5eqel ) BCDABEFBAFZGHABIBLCJK $.
    $( [7-Apr-1995] $)

  $( A class includes its restriction.  Exercise 15 of [TakeutiZaring]
     p. 25. $)
  resss $p |- ( A |` B ) C_ A $=
    ( cres cvv cxp cin df-res inss1 eqsstri ) ABCABDEZFAABGAJHI $.
    $( [2-Aug-1994] $)

  $( Commutative law for restriction. $)
  rescom $p |- ( ( A |` B ) |` C ) = ( ( A |` C ) |` B ) $=
    ( cin cres incom reseq2i resres 3eqtr4i ) ABCDZEACBDZEABECEACEBEJKABCFGABCH
    ACBHI $.
    $( [27-Mar-1998] $)

  $( Subclass theorem for restriction. $)
  ssres $p |- ( A C_ B -> ( A |` C ) C_ ( B |` C ) ) $=
    ( wss cvv cxp cin cres ssrin df-res 3sstr4g ) ABDACEFZGBLGACHBCHABLIACJBCJK
    $.
    $( [16-Aug-1994] $)

  $( Subclass theorem for restriction.  (The proof was shortened by Andrew
     Salmon, 27-Aug-2011.) $)
  ssres2 $p |- ( A C_ B -> ( C |` A ) C_ ( C |` B ) ) $=
    ( wss cvv cxp cin cres xpss1 sslin syl df-res 3sstr4g ) ABDZCAEFZGZCBEFZGZC
    AHCBHNOQDPRDABEIOQCJKCALCBLM $.
    $( [27-Aug-2011] $) $( [22-Mar-1998] $)

  $( A restriction is a relation.  Exercise 12 of [TakeutiZaring] p. 25.  (The
     proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
  relres $p |- Rel ( A |` B ) $=
    ( cres cvv cxp wss wrel cin df-res inss2 eqsstri relxp relss mp2 ) ABCZBDEZ
    FPGOGOAPHPABIAPJKBDLOPMN $.
    $( [27-Aug-2011] $) $( [2-Aug-1994] $)

  $( Absorption law for restriction.  Exercise 17 of [TakeutiZaring] p. 25. $)
  resabs1 $p |- ( B C_ C -> ( ( A |` C ) |` B ) = ( A |` B ) ) $=
    ( wss cres cin resres wceq sseqin2 reseq2 sylbi syl5eq ) BCDZACEBEACBFZEZAB
    EZACBGMNBHOPHBCINBAJKL $.
    $( [9-Aug-1994] $)

  $( Absorption law for restriction. $)
  resabs2 $p |- ( B C_ C -> ( ( A |` B ) |` C ) = ( A |` B ) ) $=
    ( wss cres rescom resabs1 syl5eq ) BCDABEZCEACEBEIABCFABCGH $.
    $( [27-Mar-1998] $)

  $( Idempotent law for restriction. $)
  residm $p |- ( ( A |` B ) |` B ) = ( A |` B ) $=
    ( wss cres wceq ssid resabs2 ax-mp ) BBCABDZBDIEBFABBGH $.
    $( [27-Mar-1998] $)

  $( A restriction to an image. $)
  resima $p |- ( ( A |` B ) " B ) = ( A " B ) $=
    ( cres crn cima residm rneqi df-ima 3eqtr4i ) ABCZBCZDJDJBEABEKJABFGJBHABHI
    $.
    $( [29-Sep-2004] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in a restriction.  (Contributed by Scott Fenton,
       17-Mar-2011.) $)
    elres $p |- ( A e. ( B |` C )
          <-> E. x e. C E. y ( A = <. x , y >. /\ <. x , y >. e. B ) ) $=
      ( cres wcel cv cop wceq wex wrex wrel relres elrel mpan eleq1 biimpd vex
      wa opelres biimpi ancomd syl6com ancld an12 syl6ib 2eximdv rexcom4 df-rex
      mpd exbii excom 3bitri sylibr biimpri biimprd syl9 imp3a exlimdv rexlimiv
      expcom impbii ) CDEFZGZCAHZBHZIZJZVHDGZTZBKZAELZVEVFEGZVKTZBKAKZVMVEVIBKA
      KZVPVDMVEVQDENABCVDOPVEVIVOABVEVIVIVNVJTZTVOVEVIVRVIVEVHVDGZVRVIVEVSCVHVD
      QZRVSVJVNVSVJVNTZVFVGDEBSUAZUBUCUDUEVIVNVJUFUGUHUKVMVKAELZBKVOAKZBKVPVKAB
      EUIWCWDBVKAEUJULVOBAUMUNUOVLVEAEVNVKVEBVNVIVJVEVNVJVSVIVEVJVNVSVSWAWBUPVB
      VIVEVSVTUQURUSUTVAVC $.
      $( [17-Mar-2011] $)

    ${
      elsnres.1 $e |- C e. _V $.
      $( Memebership in restriction to a singleton.  (Contributed by Scott
         Fenton, 17-Mar-2011.) $)
      elsnres $p |- ( A e. ( B |` { C } )
            <-> E. y ( A = <. C , y >. /\ <. C , y >. e. B ) ) $=
        ( vx csn cres wcel cv cop wceq wa wex elres rexcom4 opeq1 eqeq2d eleq1d
        wrex anbi12d rexsn exbii 3bitri ) BCDGZHIBFJZAJZKZLZUHCIZMZANFUETUKFUET
        ZANBDUGKZLZUMCIZMZANFABCUEOUKFAUEPULUPAUKUPFDEUFDLZUIUNUJUOUQUHUMBUFDUG
        QZRUQUHUMCURSUAUBUCUD $.
        $( [17-Mar-2011] $)
    $}

    $( Simplification law for restriction. $)
    relssres $p |- ( ( Rel A /\ dom A C_ B ) -> ( A |` B ) = A ) $=
      ( vx vy wrel cdm wss wa cres wceq simpl cv cop wcel vex opeldm ssel ancld
      wi syl5 opelres syl6ibr adantl relssdv resss jctil eqss sylibr ) AEZAFZBG
      ZHZABIZAGZAUMGZHUMAJULUOUNULCDAUMUIUKKUKCLZDLZMZANZURUMNZSUIUKUSUSUPBNZHU
      TUKUSVAUSUPUJNUKVAUPUQACOPUJBUPQTRUPUQABDOUAUBUCUDABUEUFUMAUGUH $.
      $( [15-Mar-2004] $) $( [16-Aug-1994] $)
  $}

  $( A relation restricted to its domain equals itself. $)
  resdm $p |- ( Rel A -> ( A |` dom A ) = A ) $=
    ( wrel cdm wss cres wceq ssid relssres mpan2 ) ABACZJDAJEAFJGAJHI $.
    $( [12-Dec-2006] $)

  $( The restriction of a set is a set.  (The proof was shortened by Andrew
     Salmon, 27-Aug-2011.) $)
  resexg $p |- ( A e. V -> ( A |` B ) e. _V ) $=
    ( cres wss wcel cvv resss ssexg mpan ) ABDZAEACFKGFABHKACIJ $.
    $( [27-Aug-2011] $) $( [28-Mar-1998] $)

  ${
    resex.1 $e |- A e. _V $.
    $( The restriction of a set is a set.  (Contributed by Jeff Madsen,
       19-Jun-2011.) $)
    resex $p |- ( A |` B ) e. _V $=
      ( cvv wcel cres resexg ax-mp ) ADEABFDECABDGH $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d x y A $.
    $( Restriction of a class abstraction of ordered pairs. $)
    resopab $p |- ( { <. x , y >. | ph } |` A ) =
                  { <. x , y >. | ( x e. A /\ ph ) } $=
      ( copab cres cvv cxp cin cv wa df-res df-xp biantru opabbii eqtr4i ineq2i
      wcel vex incom eqtri inopab 3eqtri ) ABCEZDFUDDGHZIZBJDRZBCEZUDIZUGAKBCEU
      DDLUFUDUHIUIUEUHUDUEUGCJGRZKZBCEUHBCDGMUGUKBCUJUGCSNOPQUDUHTUAUGABCUBUC
      $.
      $( [5-Nov-2002] $)

    $( The existence of a restricted identity function, proved without using
       the Axiom of Replacement (unlike ~ resfunexg ). $)
    resiexg $p |- ( A e. V -> ( _I |` A ) e. _V ) $=
      ( vx vy wcel cid cres cxp wss cvv relres cv wa cop simpr eleq1 biimpa jca
      wceq vex opelres wbr df-br ideq bitr3i anbi1i bitri opelxp 3imtr4i relssi
      xpexg anidms ssexg sylancr ) ABEZFAGZAAHZIUQJEZUPJECDUPUQFAKCLZDLZSZUSAEZ
      MZVBUTAEZMUSUTNZUPEZVEUQEVCVBVDVAVBOVAVBVDUSUTAPQRVFVEFEZVBMVCUSUTFADTZUA
      VGVAVBVGUSUTFUBVAUSUTFUCUSUTVHUDUEUFUGUSUTAAVHUHUIUJUOURAABBUKULUPUQJUMUN
      $.
      $( [13-Jan-2007] $)

    $( A subclass of the identity function is the identity function restricted
       to its domain.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    iss $p |- ( A C_ _I <-> A = ( _I |` dom A ) ) $=
      ( vx vy cid wss cdm cres wceq cv cop wcel wb wal wa ssel wi opeldm syl5bi
      vex wrel a1i jcad wbr df-br bitr3i wex eldm2 opeq2 eleq1d biimprcd sylcom
      ideq exlimdv imbi2d syl5ibcom imp3a impbid opelres syl6bbr alrimivv relss
      reli mpi relres eqrel sylancl mpbird resss sseq1 mpbiri impbii ) ADEZADAF
      ZGZHZVLVOBIZCIZJZAKZVRVNKZLZCMBMZVLWABCVLVSVRDKZVPVMKZNZVTVLVSWEVLVSWCWDA
      DVROZVSWDPVLVPVQABSZQUAUBVLWCWDVSWCVPVQHZVLWDVSPZWCVPVQDUCWHVPVQDUDVPVQCS
      ZULUEZVLWDVPVPJZAKZPWHWIWDVSCUFVLWMCVPAWGUGVLVSWMCVLVSWCWMWFWCWHVSWMWKWHW
      MVSWHWLVRAVPVQVPUHUIZUJRUKUMRWHWMVSWDWNUNUORUPUQVPVQDVMWJURUSUTVLATZVNTVO
      WBLVLDTWOVBADVAVCDVMVDBCAVNVEVFVGVOVLVNDEDVMVHAVNDVIVJVK $.
      $( [27-Aug-2011] $) $( [13-Dec-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Restriction of a class abstraction of ordered pairs. $)
    resopab2 $p |- ( A C_ B -> ( { <. x , y >. | ( x e. B /\ ph ) } |` A ) =
                  { <. x , y >. | ( x e. A /\ ph ) } ) $=
      ( wss cv wcel wa copab cres resopab wi wb ssel pm4.71 sylib anass syl6rbb
      anbi1d opabbidv syl5eq ) DEFZBGZEHZAIZBCJDKUDDHZUFIZBCJUGAIZBCJUFBCDLUCUH
      UIBCUCUIUGUEIZAIUHUCUGUJAUCUGUEMUGUJNDEUDOUGUEPQTUGUEARSUAUB $.
      $( [24-Aug-2007] $)
  $}

  ${
    $d w x y z A $.  $d w x y z R $.
    $( Alternate definition of the restriction operation.  (Contributed by
       Mario Carneiro, 5-Nov-2013.) $)
    dfres2 $p |- ( R |` A ) = { <. x , y >. | ( x e. A /\ x R y ) } $=
      ( vz vw cres cv wcel wbr wa copab wceq cop wb wal vex brres df-br wrel
      ancom 3bitr3i eleq1 breq1 anbi12d breq2 anbi2d bitr4i gen2 relres relopab
      opelopab eqrel mp2an mpbir ) DCGZAHZCIZUQBHZDJZKZABLZMZEHZFHZNZUPIZVFVBIZ
      OZFPEPZVIEFVGVDCIZVDVEDJZKZVHVDVEUPJVLVKKVGVMVDVEDCFQZRVDVEUPSVLVKUAUBVAV
      KVDUSDJZKVMABVDVEEQVNUQVDMURVKUTVOUQVDCUCUQVDUSDUDUEUSVEMVOVLVKUSVEVDDUFU
      GULUHUIUPTVBTVCVJODCUJVAABUKEFUPVBUMUNUO $.
      $( [5-Nov-2013] $)
  $}

  ${
    $d A x y $.
    $( The restricted identity expressed with the class builder.  (Contributed
       by FL, 25-Apr-2012.) $)
    opabresid $p |- { <. x , y >. | ( x e. A /\ y = x ) } = ( _I |` A ) $=
      ( weq copab cres cv wcel wa cid resopab wceq equcom opabbii eqtr4i reseq1
      dfid3 ax-mp eqtr3i ) BADZABEZCFZAGCHTIABEJCFZTABCKUAJLUBUCLUAABDZABEJTUDA
      BBAMNABQOUAJCPRS $.
      $( [25-Apr-2012] $)
  $}

  $( The domain of a restricted identity function. $)
  dmresi $p |- dom ( _I |` A ) = A $=
    ( cid cdm wss cres wceq cvv ssv dmi sseqtr4i ssdmres mpbi ) ABCZDBAECAFAGMA
    HIJABKL $.
    $( [27-Aug-2004] $)

  $( MAY BE REVISED - delete this and replace w/ dfres3 (in FL's mathbox) $)
  $( Any relation restricted to the universe is itself. $)
  resid $p |- ( Rel A -> ( A |` _V ) = A ) $=
    ( wrel cdm cvv wss cres wceq ssv relssres mpan2 ) ABACZDEADFAGKHADIJ $.
    $( [16-Mar-2004] $)

  $( Equality theorem for image. $)
  imaeq1 $p |- ( A = B -> ( A " C ) = ( B " C ) ) $=
    ( wceq cres crn cima reseq1 rneqd df-ima 3eqtr4g ) ABDZACEZFBCEZFACGBCGLMNA
    BCHIACJBCJK $.
    $( [14-Aug-1994] $)

  $( Equality theorem for image. $)
  imaeq2 $p |- ( A = B -> ( C " A ) = ( C " B ) ) $=
    ( wceq cres crn cima reseq2 rneqd df-ima 3eqtr4g ) ABDZCAEZFCBEZFCAGCBGLMNA
    BCHICAJCBJK $.
    $( [14-Aug-1994] $)

  ${
    imaeq1i.1 $e |- A = B $.
    $( Equality theorem for image. $)
    imaeq1i $p |- ( A " C ) = ( B " C ) $=
      ( wceq cima imaeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [21-Dec-2008] $)

    $( Equality theorem for image. $)
    imaeq2i $p |- ( C " A ) = ( C " B ) $=
      ( wceq cima imaeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [21-Dec-2008] $)
  $}

  ${
    imaeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality theorem for image.  (Contributed by FL, 15-Dec-2006.) $)
    imaeq1d $p |- ( ph -> ( A " C ) = ( B " C ) ) $=
      ( wceq cima imaeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [12-Dec-2006] $)

    $( Equality theorem for image.  (Contributed by FL, 15-Dec-2006.) $)
    imaeq2d $p |- ( ph -> ( C " A ) = ( C " B ) ) $=
      ( wceq cima imaeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [12-Dec-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Alternate definition of image.  Compare definition (d) of [Enderton]
       p. 44.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    dfima2 $p |- ( A " B ) = { y | E. x e. B x A y } $=
      ( cima cres crn cv wbr wex cab wrex df-ima dfrn2 wa vex brres ancom bitri
      wcel exbii df-rex bitr4i abbii 3eqtri ) CDECDFZGAHZBHZUFIZAJZBKUGUHCIZADL
      ZBKCDMABUFNUJULBUJUGDTZUKOZAJULUIUNAUIUKUMOUNUGUHCDBPQUKUMRSUAUKADUBUCUDU
      E $.
      $( [27-Aug-2011] $) $( [19-Apr-2004] $)

    $( Alternate definition of image.  Compare definition (d) of [Enderton]
       p. 44.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    dfima3 $p |- ( A " B ) = { y | E. x ( x e. B /\ <. x , y >. e. A ) } $=
      ( cima cv wbr wrex cab wcel cop wa dfima2 df-br rexbii df-rex bitri abbii
      wex eqtri ) CDEAFZBFZCGZADHZBIUADJUAUBKCJZLASZBIABCDMUDUFBUDUEADHUFUCUEAD
      UAUBCNOUEADPQRT $.
      $( [27-Aug-2011] $) $( [14-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elimag $p |- ( A e. V -> ( A e. ( B " C ) <-> E. x e. C x B A ) ) $=
      ( vy cv wbr wrex cima wceq breq2 rexbidv dfima2 elab2g ) AGZFGZCHZADIPBCH
      ZADIFBCDJEQBKRSADQBPCLMAFCDNO $.
      $( [20-Jan-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    elima.1 $e |- A e. _V $.
    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima $p |- ( A e. ( B " C ) <-> E. x e. C x B A ) $=
      ( cvv wcel cima cv wbr wrex wb elimag ax-mp ) BFGBCDHGAIBCJADKLEABCDFMN
      $.
      $( [19-Apr-2004] $)

    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima2 $p |- ( A e. ( B " C ) <-> E. x ( x e. C /\ x B A ) ) $=
      ( cima wcel cv wbr wrex wa wex elima df-rex bitri ) BCDFGAHZBCIZADJPDGQKA
      LABCDEMQADNO $.
      $( [11-Aug-2004] $)

    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima3 $p |- ( A e. ( B " C ) <-> E. x ( x e. C /\ <. x , A >. e. B ) ) $=
      ( cima wcel cv wbr wa wex cop elima2 df-br anbi2i exbii bitri ) BCDFGAHZD
      GZRBCIZJZAKSRBLCGZJZAKABCDEMUAUCATUBSRBCNOPQ $.
      $( [14-Aug-1994] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z w $.
    hbima.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbima.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for image.  (The proof was shortened
       by Andrew Salmon, 27-Aug-2011.) $)
    hbima $p |- ( y e. ( A " B ) -> A. x y e. ( A " B ) ) $=
      ( cima cres crn df-ima hbres hbrn hbxfreq ) ABCDGCDHZICDJABNABCDEFKLM $.
      $( [27-Aug-2011] $) $( [30-Dec-1996] $)
  $}

  ${
    $d x y z $.  $d B y z $.  $d A y z $.  $d ph y $.
    hbimad.1 $e |- ( ph -> A. x ph ) $.
    hbimad.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbimad.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbima .
       (Contributed by FL, 15-Dec-2006.) $)
    hbimad $p |- ( ph -> ( y e. ( A " B ) -> A. x y e. ( A " B ) ) ) $=
      ( vz cv wcel wal cab cima wi hba1 hbab wceq alrimiv abidhb a1i syl eleq2d
      hbima imaeq1d imaeq2d eqtrd albid 3imtr3d ) ACJZIJZDKZBLZIMZUKEKZBLZIMZNZ
      KZUSBLZUJDENZKZVBBLUSUTOABCUNUQUMBICULBPQUPBICUOBPQUDUAAURVAUJAURDUQNVAAU
      NDUQAUJDKZVCBLOZCLUNDRAVDCGSBCIDTUBUEAUQEDAUJEKZVEBLOZCLUQERAVFCHSBCIETUB
      UFUGUCZAUSVBBFVGUHUI $.
      $( [14-Dec-2006] $)
  $}

  ${
    $d A y z $.  $d B y z $.  $d C y z $.  $d x y z $.  $d F y z $.
    $( Move class substitution in and out of the image of a function.
       (Contributed by FL, 15-Dec-2006.) $)
    csbima12g $p |- ( A e. C -> [_ A / x ]_ ( F " B ) =
                 ( [_ A / x ]_ F " [_ A / x ]_ B ) ) $=
      ( vy vz wcel cv cima ax-17 hbcsb1g hbcsb1 csbeq1a imaeq1d imaeq2d 3eqtr3d
      csb wceq csbcog hbimad wex a9e vex hbima hbeq exlimi ax-mp 3eqtrd csbiegf
      a1i eqtrd ) BDHZFBAFIZECJZRZRFBAUNERZRZFBAUNCRZRZJZABUORABERZABCRZJZFGBUP
      VADUMFGURUTUMFKFGBUQDGIZBHFKZLFGBUSDVFLUAUNBSZUPUQUSJZURUSJVAUPVHSZVGAIUN
      SZAUBVIAFUCVJVIAAGGUPVHAGUNUOFUDZVEUNHAKZMAGUQUSAGUNEVKVLMAGUNCVKVLMUEUFV
      JUOUQCJUPVHVJEUQCAUNENOAUNUONVJCUSUQAUNCNPQUGUHUKVGUQURUSFBUQNOVGUSUTURFB
      USNPUIUJAFBUODTUMVAVBUTJVDUMURVBUTAFBEDTOUMUTVCVBAFBCDTPULQ $.
      $( [13-Dec-2006] $)
  $}

  $( Move class substitution in and out of the image of a function.(This is
     ~ csbima12g with a shortened proof, shortened by Alan Sare, 10-Nov-2012.)
     The proof is derived from the virtual deduction proof ~ csbima12gALTVD .
     Although the proof is shorter, the total number of steps of all theorems
     used in the proof is probably longer. $)
  csbima12gALT $p |- ( A e. C -> [_ A / x ]_ ( F " B ) =
                 ( [_ A / x ]_ F " [_ A / x ]_ B ) ) $=
    ( wcel cima csb cres crn df-ima csbeq2i csbrng csbresg rneqd 3eqtrd syl6eqr
    ) BDFZABECGZHZABEHZABCHZIZJZUAUBGRTABECIZJZHABUEHZJUDABSUFDECKLABUEDMRUGUCA
    BECDNOPUAUBKQ $.
    $( [10-Nov-2012] $)

  ${
    $d x y A $.  $d x y B $.
    $( The image of the domain of a class is the range of the class. $)
    imadmrn $p |- ( A " dom A ) = ran A $=
      ( vx vy cv cdm wcel cop wa wex cab cima crn vex opeldm ancom bitr2i exbii
      pm4.71i abbii dfima3 dfrn3 3eqtr4i ) BDZAEZFZUCCDZGAFZHZBIZCJUGBIZCJAUDKA
      LUIUJCUHUGBUGUGUEHUHUGUEUCUFABMNRUGUEOPQSBCAUDTBCAUAUB $.
      $( [14-Aug-1994] $)

    $( The image of a class is a subset of its range.  Theorem 3.16(xi) of
       [Monk1] p. 39. $)
    imassrn $p |- ( A " B ) C_ ran A $=
      ( vx vy cv wcel cop wa wex cab cima crn simpr eximi ss2abi dfima3 3sstr4i
      dfrn3 ) CEZBFZSDEGAFZHZCIZDJUACIZDJABKALUCUDDUBUACTUAMNOCDABPCDARQ $.
      $( [31-Mar-1995] $)
  $}

  $( The image of a set is a set.  Theorem 3.17 of [Monk1] p. 39. $)
  imaexg $p |- ( A e. V -> ( A " B ) e. _V ) $=
    ( wcel cima crn wss cvv imassrn rnexg ssexg sylancr ) ACDABEZAFZGNHDMHDABIA
    CJMNHKL $.
    $( [24-Jul-1995] $)

  ${
    $d x y A $.
    $( Image under the identity relation.  Theorem 3.16(viii) of [Monk1]
       p. 38. $)
    imai $p |- ( _I " A ) = A $=
      ( vx vy cid cima cv wcel cop wa wex cab dfima3 wceq wbr df-br ideq bitr3i
      vex anbi2i bitri ancom exbii eleq1 ceqsexv abbii abid2 3eqtri ) DAEBFZAGZ
      UHCFZHDGZIZBJZCKUJAGZCKABCDALUMUNCUMUHUJMZUIIZBJUNULUPBULUIUOIUPUKUOUIUKU
      HUJDNUOUHUJDOUHUJCRZPQSUIUOUATUBUIUNBUJUQUHUJAUCUDTUECAUFUG $.
      $( [30-Apr-1998] $)
  $}

  $( The range of the restricted identity function. $)
  rnresi $p |- ran ( _I |` A ) = A $=
    ( cid cima cres crn df-ima imai eqtr3i ) BACBADEABAFAGH $.
    $( [27-Aug-2004] $)

  $( The image of a restriction of the identity function.  (Contributed by FL,
     31-Dec-2006.) $)
  resiima $p |- ( B C_ A -> ( ( _I |` A ) " B ) = B ) $=
    ( wss cid cres cima crn wceq df-ima a1i resabs1 rneqd rnresi 3eqtrd ) BACZD
    AEZBFZPBEZGZDBEZGZBQSHOPBIJORTDBAKLUABHOBMJN $.
    $( [28-Dec-2006] $)

  $( Image of the empty set.  Theorem 3.16(ii) of [Monk1] p. 38. $)
  ima0 $p |- ( A " (/) ) = (/) $=
    ( c0 cima cres crn df-ima res0 rneqi rn0 3eqtri ) ABCABDZEBEBABFKBAGHIJ $.
    $( [20-May-1998] $)

  $( Image under the empty relation.  (Contributed by FL, 11-Jan-2007.) $)
  0ima $p |- ( (/) " A ) = (/) $=
    ( c0 cima crn imassrn rn0 sseqtri 0ss eqssi ) BACZBJBDBBAEFGJHI $.
    $( [4-Jan-2007] $)

  $( A class whose image under another is empty is disjoint with the other's
     domain.  (Contributed by FL, 24-Jan-2007.) $)
  imadisj $p |- ( ( A " B ) = (/) <-> ( dom A i^i B ) = (/) ) $=
    ( cima wceq cres crn cdm cin df-ima eqeq1i dm0rn0 dmres incom eqtri 3bitr2i
    c0 ) ABCZPDABEZFZPDRGZPDAGZBHZPDQSPABIJRKTUBPTBUAHUBABLBUAMNJO $.
    $( [15-Jan-2007] $)

  $( A pre-image under any class is included in the domain of the class.
     (Contributed by FL, 29-Jan-2007.) $)
  cnvimass $p |- ( `' A " B ) C_ dom A $=
    ( ccnv cima crn cdm imassrn dfdm4 sseqtr4i ) ACZBDJEAFJBGAHI $.
    $( [29-Jan-2007] $)

  $( The pre-image of the range of a class is the domain of the class.
     (Contributed by Jeff Hankins, 15-Jul-2009.) $)
  cnvimarndm $p |- ( `' A " ran A ) = dom A $=
    ( ccnv cdm cima crn imadmrn df-rn imaeq2i dfdm4 3eqtr4i ) ABZKCZDKEKAEZDACK
    FMLKAGHAIJ $.
    $( [12-Jul-2009] $)

  ${
    $d y x A $.  $d y x R $.
    $( The image of a singleton. $)
    imasng $p |- ( A e. B -> ( R " { A } ) = { y | A R y } ) $=
      ( vx wcel cvv csn cima wbr cab wceq elex wrex dfima2 rexsng abbidv syl5eq
      cv breq1 syl ) BCFBGFZDBHZIZBASZDJZAKZLBCMUBUDESZUEDJZEUCNZAKUGEADUCOUBUJ
      UFAUIUFEBGUHBUEDTPQRUA $.
      $( [8-May-2005] $)

    $( The image of a singleton. $)
    relimasn $p |- ( Rel R -> ( R " { A } ) = { y | A R y } ) $=
      ( wrel cvv wcel csn cima cv wbr cab wceq wn wa c0 snprc imaeq2 sylbi ima0
      ex syl6eq adantl wex brrelex con3d imp nexdv abn0 necon1bbii sylib eqtr4d
      imasng pm2.61d2 ) CDZBEFZCBGZHZBAIZCJZAKZLZUNUOMZVAUNVBNZUQOUTVBUQOLUNVBU
      QCOHZOVBUPOLUQVDLBPUPOCQRCSUAUBVCUSAUCZMUTOLVCUSAUNVBUSMUNUSUOUNUSUOBURCU
      DTUEUFUGVEUTOUSAUHUIUJUKTABECULUM $.
      $( [10-May-2005] $) $( [20-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    elimasn.1 $e |- B e. _V $.
    elimasn.2 $e |- C e. _V $.
    $( Membership in an image of a singleton.  (The proof was shortened by
       Andrew Salmon, 27-Aug-2011.) $)
    elimasn $p |- ( C e. ( A " { B } ) <-> <. B , C >. e. A ) $=
      ( vx csn cima wcel wbr cop cv breq2 cvv cab wceq imasng ax-mp elab2 df-br
      bitri ) CABGHZIBCAJZBCKAIBFLZAJZUCFCUBEUDCBAMBNIUBUEFOPDFBNAQRSBCATUA $.
      $( [27-Aug-2011] $) $( [15-Mar-2004] $)
  $}

  ${
    $d A y z $.  $d B y z $.  $d C y z $.
    $( Membership in an image of a singleton.  (Contributed by Raph Levien,
       21-Oct-2006.) $)
    elimasng $p |- ( ( B e. V /\ C e. W ) ->
                   ( C e. ( A " { B } ) <-> <. B , C >. e. A ) ) $=
      ( vz vy cv csn cima wcel cop wceq sneq imaeq2d eleq2d eleq1d bibi12d vex
      wb opeq1 eleq1 opeq2 elimasn vtocl2g ) FHZAGHZIZJZKZUGUFLZAKZTUFABIZJZKZB
      UFLZAKZTCUNKZBCLZAKZTGFBCDEUGBMZUJUOULUQVAUIUNUFVAUHUMAUGBNOPVAUKUPAUGBUF
      UAQRUFCMZUOURUQUTUFCUNUBVBUPUSAUFCBUCQRAUGUFGSFSUDUE $.
      $( [21-Oct-2006] $)
  $}

  $( Membership in an image of a singleton. $)
  elimasni $p |- ( C e. ( A " { B } ) -> B A C ) $=
    ( cvv wcel wa csn cima wbr wn c0 noel wceq snprc biimpi imaeq2d ima0 syl6eq
    eleq2d mtbiri con4i elex jca cop elimasng df-br syl6bbr biimpd mpcom ) BDEZ
    CDEZFZCABGZHZEZBCAIZUOUJUKUJUOUJJZUOCKECLUQUNKCUQUNAKHKUQUMKAUQUMKMBNOPAQRS
    TUACUNUBUCULUOUPULUOBCUDAEUPABCDDUEBCAUFUGUHUI $.
    $( [5-Aug-2010] $)

  ${
    $d y F $.  $d x y $.
    $( Two ways to express the class of unique-valued arguments of ` F ` ,
       which is the same as the domain of ` F ` whenever ` F ` is a function.
       The left-hand side of the equality is from Definition 10.2 of [Quine]
       p. 65.  Quine uses the notation "arg ` F ` " for this class (for which
       we have no separate notation).  Observe the resemblance to our ~ df-fv ,
       which was based on the idea in Quine's definition. $)
    args $p |- { x | E. y ( F " { x } ) = { y } } = { x | E! y x F y } $=
      ( cv csn cima wceq wex wbr weu cab cvv wcel vex imasng ax-mp eqeq1i exbii
      euabsn bitr4i abbii ) CADZEFZBDZEZGZBHZUBUDCIZBJZAUGUHBKZUEGZBHUIUFUKBUCU
      JUEUBLMUCUJGANBUBLCOPQRUHBSTUA $.
      $( [8-May-2005] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    eliniseg.1 $e |- C e. _V $.
    $( Membership in an initial segment.  The idiom ` ( ``' A " { B } ) ` ,
       meaning ` { x | x A B } ` , is used to specify an initial segment in
       (for example) Definition 6.21 of [TakeutiZaring] p. 30.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    eliniseg $p |- ( B e. V -> ( C e. ( `' A " { B } ) <-> C A B ) ) $=
      ( wcel cvv ccnv csn cima wbr wb wa cop elimasng df-br syl6bbr bitrd mpan2
      brcnvg ) BDFZCGFZCAHZBIJFZCBAKZLEUAUBMZUDBCUCKZUEUFUDBCNUCFUGUCBCDGOBCUCP
      QBCDGATRS $.
      $( [27-Aug-2011] $) $( [28-Apr-2004] $)
  $}

  ${
    $d A x y $.
    epini.1 $e |- A e. _V $.
    $( Any set is equal to its preimage under the converse epsilon relation.
       (Contributed by Mario Carneiro, 9-Mar-2013.) $)
    epini $p |- ( `' _E " { A } ) = A $=
      ( vx cep ccnv csn cima cv wbr cvv wb vex eliniseg ax-mp epelc bitri eqriv
      wcel ) CDEAFGZACHZSRZTADIZTARAJRUAUBKBDATJCLZMNTAUCBOPQ $.
      $( [9-Mar-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An idiom that signifies an initial segment of an ordering, used, for
       example, in Definition 6.21 of [TakeutiZaring] p. 30. $)
    iniseg $p |- ( B e. V -> ( `' A " { B } ) = { x | x A B } ) $=
      ( wcel cvv ccnv csn cima cv wbr cab wceq elex vex eliniseg abbi2dv syl )
      CDECFEZBGCHIZAJZCBKZALMCDNSUBATBCUAFAOPQR $.
      $( [28-Apr-2004] $)
  $}

  ${
    $d x y z R $.  $d x A $.
    $( Alternate definition of founded relation.  Definition 6.21 of
       [TakeutiZaring] p. 30. $)
    dffr3 $p |- ( R Fr A <-> A. x ( ( x C_ A /\ x =/= (/) ) ->
                E. y e. x ( x i^i ( `' R " { y } ) ) = (/) ) ) $=
      ( vz wfr cv wss c0 wne wa wbr cab cin wceq wrex wi wal ccnv cvv csn dffr2
      cima wcel vex iniseg ax-mp ineq2i eqeq1i rexbii imbi2i albii bitr4i ) CDF
      AGZCHUNIJKZUNEGBGZDLEMZNZIOZBUNPZQZARUOUNDSUPUAUCZNZIOZBUNPZQZARABECDUBVF
      VAAVEUTUOVDUSBUNVCURIVBUQUNUPTUDVBUQOBUEEDUPTUFUGUHUIUJUKULUM $.
      $( [3-Feb-2007] $) $( [23-Apr-2004] $)
  $}

  $( Subset theorem for image. $)
  imass1 $p |- ( A C_ B -> ( A " C ) C_ ( B " C ) ) $=
    ( wss cres crn cima ssres rnss syl df-ima 3sstr4g ) ABDZACEZFZBCEZFZACGBCGM
    NPDOQDABCHNPIJACKBCKL $.
    $( [16-Mar-2004] $)

  $( Subset theorem for image.  Exercise 22(a) of [Enderton] p. 53. $)
  imass2 $p |- ( A C_ B -> ( C " A ) C_ ( C " B ) ) $=
    ( wss cres crn cima ssres2 rnss syl df-ima 3sstr4g ) ABDZCAEZFZCBEZFZCAGCBG
    MNPDOQDABCHNPIJCAKCBKL $.
    $( [22-Mar-1998] $)

  $( The image of a singleton outside the domain is empty. $)
  ndmima $p |- ( -. A e. dom B -> ( B " { A } ) = (/) ) $=
    ( cdm wcel wn csn cima cres crn c0 df-ima wceq cin dmres incom eqtri disjsn
    biimpri syl5eq dm0rn0 sylib ) ABCZDEZBAFZGBUDHZIZJBUDKUCUECZJLUFJLUCUGUBUDM
    ZJUGUDUBMUHBUDNUDUBOPUHJLUCUBAQRSUETUAS $.
    $( [22-May-1998] $)

  ${
    $d x y A $.
    $( A converse is a relation.  Theorem 12 of [Suppes] p. 62. $)
    relcnv $p |- Rel `' A $=
      ( vy vx ccnv wrel cv wbr copab relopab df-cnv releqi mpbir ) ADZEBFCFAGZC
      BHZENCBIMOCBAJKL $.
      $( [29-Oct-1996] $)
  $}

  ${
    $d x y z R $.
    $( Two ways of saying a relation is transitive.  Definition of transitivity
       in [Schechter] p. 51.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    cotr $p |- ( ( R o. R ) C_ R <->
             A. x A. y A. z ( ( x R y /\ y R z ) -> x R z ) ) $=
      ( ccom wss cv cop wcel wi wal wbr wa wrel wb wex copab vex albii bitri
      relopab df-co releqi mpbir ssrel ax-mp opelco df-br bicomi imbi12i 19.23v
      bitr4i alcom ) DDEZDFZAGZCGZHZUNIZURDIZJZCKZAKZUPBGZDLVDUQDLMZUPUQDLZJZCK
      BKZAKUNNZUOVCOVIVEBPZACQZNVJACUAUNVKACBDDUBUCUDACUNDUEUFVBVHAVBVGBKZCKVHV
      AVLCVAVJVFJVLUSVJUTVFBUPUQDDARCRUGVFUTUPUQDUHUIUJVEVFBUKULSVGCBUMTST $.
      $( [27-Aug-2011] $) $( [27-Dec-1996] $)
  $}

  ${
    $d x y R $.
    $( Two ways of saying a relation is symmetric.  Similar to definition of
       symmetry in [Schechter] p. 51.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    cnvsym $p |- ( `' R C_ R <-> A. x A. y ( x R y -> y R x ) ) $=
      ( cv cop ccnv wcel wi wal wss wbr alcom wrel relcnv ssrel ax-mp vex brcnv
      wb df-br bitr3i imbi12i 2albii 3bitr4i ) BDZADZEZCFZGZUGCGZHZAIBIZUKBIAIU
      HCJZUFUECKZUEUFCKZHZBIAIUKBALUHMUMULSCNBAUHCOPUPUKABUNUIUOUJUNUEUFUHKUIUE
      UFCBQAQRUEUFUHTUAUEUFCTUBUCUD $.
      $( [27-Aug-2011] $) $( [28-Dec-1996] $)

    $( Two ways of saying a relation is antisymmetric.  Definition of
       antisymmetry in [Schechter] p. 51.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    intasym $p |- ( ( R i^i `' R ) C_ _I <->
                  A. x A. y ( ( x R y /\ y R x ) -> x = y ) ) $=
      ( ccnv cin cid wss cv cop wcel wi wal wbr wa wceq wrel ax-mp df-br bitr3i
      vex wb relcnv relin2 ssrel elin brcnv anbi12i bitr4i imbi12i 2albii bitri
      ideq ) CCDZEZFGZAHZBHZIZUNJZURFJZKZBLALZUPUQCMZUQUPCMZNZUPUQOZKZBLALUNPZU
      OVBUAUMPVHCUBCUMUCQABUNFUDQVAVGABUSVEUTVFUSURCJZURUMJZNVEURCUMUEVCVIVDVJU
      PUQCRVDUPUQUMMVJUPUQCATBTZUFUPUQUMRSUGUHUTUPUQFMVFUPUQFRUPUQVKULSUIUJUK
      $.
      $( [27-Aug-2011] $) $( [9-Sep-2004] $)

    $( Two ways of saying a relation is antisymmetric and reflexive.
       ` U. U. R ` is the field of a relation by ~ relfld .  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    asymref $p |- ( ( R i^i `' R ) = ( _I |` U. U. R ) <->
       A. x e. U. U. R A. y ( ( x R y /\ y R x ) <-> x = y ) ) $=
      ( cv cop wcel cid cuni wb wal wbr wa wceq wi df-br vex 3bitr4i albii wrel
      bitr3i ccnv cin cres wral opeluu sylbi simpld adantr pm4.71ri bibi1i elin
      brcnv anbi12i bitr4i opelres ancom ideq anbi2i 3bitri pm5.32 19.21v bitri
      bibi12i relcnv relin2 ax-mp relres eqrel mp2an df-ral ) ADZBDZEZCCUAZUBZF
      ZVMGCHHZUCZFZIZBJZAJZVKVQFZVKVLCKZVLVKCKZLZVKVLMZIZBJZNZAJVOVRMZWIAVQUDWA
      WJAWAWCWHNZBJWJVTWLBWFWCWGLZIWCWFLZWMIVTWLWFWNWMWFWCWDWCWEWDWCVLVQFZWDVMC
      FZWCWOLVKVLCOZABCUEUFUGUHUIUJVPWFVSWMVPWPVMVNFZLWFVMCVNUKWDWPWEWRWQWEVKVL
      VNKWRVKVLCAPBPZULVKVLVNOTUMUNVSVMGFZWCLWCWTLWMVKVLGVQWSUOWTWCUPWTWGWCWTVK
      VLGKWGVKVLGOVKVLWSUQTURUSVCWCWFWGUTQRWCWHBVAVBRVOSZVRSWKWBIVNSXACVDCVNVEV
      FGVQVGABVOVRVHVIWIAVQVJQ $.
      $( [27-Aug-2011] $) $( [6-May-2008] $)

    $( Two ways of saying a relation is antisymmetric and reflexive.  (The
       proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    asymref2 $p |- ( ( R i^i `' R ) = ( _I |` U. U. R ) <->
   ( A. x e. U. U. R x R x /\ A. x A. y ( ( x R y /\ y R x ) -> x = y ) ) ) $=
      ( ccnv cin cid cuni cres wceq cv wbr wa wb wal wral asymref ralbii 3bitri
      wi wcel albiim r19.26 ancom equcom imbi1i albii ax-17 breq2 breq1 anbi12d
      anidm syl6bb equsal bitri hbra1 ralcom4 cop opeluu simpld ra4 syl5 syl5bi
      df-br adantrd pm2.43d alimi sylbi alrimi alral impbii anbi12i ) CCDEFCGGZ
      HIAJZBJZCKZVNVMCKZLZVMVNIZMBNZAVLOVQVRSZBNZVRVQSZBNZLZAVLOZVMVMCKZAVLOZWA
      ANZLZABCPVSWDAVLVQVRBUAQWEWAAVLOZWCAVLOZLWKWJLWIWAWCAVLUBWJWKUCWKWGWJWHWC
      WFAVLWCVNVMIZVQSZBNWFWBWMBVRWLVQABUDUEUFVQWFBAWFBUGWLVQWFWFLWFWLVOWFVPWFV
      NVMVMCUHVNVMVMCUIUJWFUKULUMUNQWJWHWJWAAWAAVLUOWJVTAVLOZBNWAVTABVLUPWNVTBW
      NVQVRWNVOVTVPVOVMVNUQCTZWNVTVMVNCVCWOVMVLTZWNVTWOWPVNVLTABCURUSVTAVLUTVAV
      BVDVEVFVGVHWAAVLVIVJVKRR $.
      $( [27-Aug-2011] $) $( [6-May-2008] $)

    $( Two ways of saying a relation is irreflexive.  Definition of
       irreflexivity in [Schechter] p. 51.  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    intirr $p |- ( ( R i^i _I ) = (/) <-> A. x -. x R x ) $=
      ( vy cid cin c0 wceq cv cop wcel cvv cdif wi wal wbr wn wss incom 3bitr2i
      df-br eqeq1i disj2 wrel wb reli ssrel ax-mp 3bitri equcom vex wa biantrur
      ideq notbii eldif 3bitr4i imbi12i 2albii ax-17 breq2 notbid equsal albii
      opex ) BDEZFGZAHZCHZIZDJZVIKBLZJZMZCNANZVHVGGZVGVHBOZPZMZCNZANVGVGBOZPZAN
      VFDBEZFGDVKQZVNVEWBFBDRUADBUBDUCWCVNUDUEACDVKUFUGUHVRVMACVOVJVQVLVOVGVHGV
      GVHDOVJCAUIVGVHCUJUMVGVHDTSVIBJZPZVIKJZWEUKVQVLWFWEVGVHVDULVPWDVGVHBTUNVI
      KBUOUPUQURVSWAAVQWACAWACUSVOVPVTVHVGVGBUTVAVBVCS $.
      $( [27-Aug-2011] $) $( [9-Sep-2004] $)
  $}

  ${
    $d A x $.  $d R x $.
    irrexi.1 $e |- R C_ ( S X. S ) $.
    irrexi.2 $e |- -. x R x $.
    $( An irreflexive relation guarantees the existence of its arguments (even
       the right one, unlike reflexive relations, due to ~ brprc ).
       (Contributed by Mario Carneiro, 10-May-2013.) $)
    irrexi $p |- ( A R B -> ( A e. S /\ B e. S ) ) $=
      ( cvv wcel wbr wa wn wrel cxp wss xpss sstri df-rel mpbir brrelexi breq12
      cv wceq wb anidms notbid vtoclg syl brprc biimpcd mt3d brelg mpcom ) CHIZ
      BCDJZBEICEIKUOUNBBDJZUOBHIUPLZBCDDMDHHNZODEENURFEEPQDRSTAUBZUSDJZLUQABHUS
      BUCZUTUPVAUTUPUDUSBUSBDUAUEUFGUGUHUNLUOUPBCDUIUJUKBCEEDHFULUM $.
      $( [10-May-2013] $) $( [10-Feb-1996] $)
  $}

  ${
    soi.1 $e |- R Or S $.
    soi.2 $e |- R C_ ( S X. S ) $.
    $( A strict order relation is irreflexive.  (Revised by Mario Carneiro,
       10-May-2013.) $)
    soirri $p |- -. A R A $=
      ( wcel wa wbr wn wor sonr mpan adantl cvv wrel cxp wss xpss sstri df-rel
      mpbir brrelexi brelg mpcom con3i pm2.61i ) ACFZUGGZAABHZIZUGUJUGCBJUGUJDC
      ABKLMUIUHANFUIUHAABBOBNNPZQBCCPUKECCRSBTUAUBAACCBNEUCUDUEUF $.
      $( [10-May-2013] $) $( [10-Feb-1996] $)

    ${
      $d A x $.  $d B x $.  $d R x $.
      $( A strict order relation is a transitive relation.  (Revised by Mario
         Carneiro, 10-May-2013.) $)
      sotri $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( vx wcel wa wbr cv soirri irrexi simpld anim12i wi wor w3a sotr 3expb
        mpan mpcom ) AEIZBEIZCEIZJZJABDKZBCDKZJZACDKZUHUDUIUGUHUDUEHABDEGHLDEFG
        MZNOHBCDEGULNPUDUEUFUJUKQZEDRUDUEUFSUMFEABCDTUBUAUC $.
        $( [10-May-2013] $) $( [10-Feb-1996] $)

      $( A strict order relation has no 2-cycle loops.  (Revised by Mario
         Carneiro, 10-May-2013.) $)
      son2lpi $p |- -. ( A R B /\ B R A ) $=
        ( wbr wa soirri sotri mto ) ABCGBACGHAACGACDEFIABACDEFJK $.
        $( [10-May-2013] $) $( [10-Feb-1996] $)

      $( A transitivity relation.  (Read ` A <_ B ` and ` B < C ` implies
         ` A < C ` .)  (Contributed by Mario Carneiro, 10-May-2013.) $)
      sotri2 $p |- ( ( A e. S /\ -. B R A /\ B R C ) -> A R C ) $=
        ( vx wcel wbr wn wi cv soirri irrexi simpld wa wceq wo wor mpan con2bid
        wb sotric breq1 biimpd sotri ex jaoi syl6bir com3r mpand com3l 3imp ) A
        EIZBADJZKZBCDJZACDJZURUOUQUSURBEIZUOUQUSLURUTCEIHBCDEGHMDEFGNOPUTUOQZUQ
        URUSVAUQBARZABDJZSZURUSLZVAUPVDEDTVAUPVDKUCFEBADUDUAUBVBVEVCVBURUSBACDU
        EUFVCURUSABCDEFGUGUHUIUJUKULUMUN $.
        $( [10-May-2013] $)

      $( A transitivity relation.  (Read ` A < B ` and ` B <_ C ` implies
         ` A < C ` .)  (Contributed by Mario Carneiro, 10-May-2013.) $)
      sotri3 $p |- ( ( C e. S /\ A R B /\ -. C R B ) -> A R C ) $=
        ( vx wcel wbr wn wi cv soirri irrexi simprd wa wceq wo wor mpan con2bid
        sotric breq2 biimprd sotri expcom jaoi syl6bir com3r mpan2d com12 3imp
        wb ) CEIZABDJZCBDJZKZACDJZUPUOURUSLZUPUOBEIZUTUPAEIVAHABDEGHMDEFGNOPUOV
        AQZURUPUSVBURCBRZBCDJZSZUPUSLZVBUQVEEDTVBUQVEKUNFECBDUCUAUBVCVFVDVCUSUP
        CBADUDUEUPVDUSABCDEFGUFUGUHUIUJUKULUM $.
        $( [10-May-2013] $)
    $}
  $}

  ${
    soiOLD.1 $e |- A e. _V $.
    soiOLD.2 $e |- R Or S $.
    soiOLD.3 $e |- R C_ ( S X. S ) $.
    $( A strict order relation is irreflexive. $)
    soirriOLD $p |- -. A R A $=
      ( wcel wa wbr wn wor sonr mpan adantl brel con3i pm2.61i ) ACGZRHZAABIZJZ
      RUARCBKRUAECABLMNTSAACCBDFOPQ $.
      $( [10-Feb-1996] $)

    ${
      sotriOLD.4 $e |- B e. _V $.
      sotriOLD.5 $e |- C e. _V $.
      $( A strict order relation is a transitive relation. $)
      sotriOLD $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( wcel w3a wbr wa brel wi id 3exp a1dd imp43 syl2an wor sotr mpan mpcom
        ) AEKZBEKZCEKZLZABDMZBCDMZNZACDMZUJUFUGNUGUHNUIUKABEEDIHOBCEEDJHOUFUGUG
        UHUIUFUGUHUIPUGUFUGUHUIUIQRSTUAEDUBUIULUMPGEABCDUCUDUE $.
        $( [10-Feb-1996] $)
    $}

    ${
      son2lpiOLD.4 $e |- B e. _V $.
      $( A strict order relation has no 2-cycle loops. $)
      son2lpiOLD $p |- -. ( A R B /\ B R A ) $=
        ( wbr wa soirri sotri mto ) ABCIBACIJAACIACDFGKABACDFGLM $.
        $( [10-Feb-1996] $)
    $}
  $}

  ${
    $d x y z w $.  $d z w ph $.
    $( The converse of a class abstraction of ordered pairs.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    cnvopab $p |- `' { <. x , y >. | ph } = { <. y , x >. | ph } $=
      ( vz vw copab ccnv relcnv relopab cv cop wcel opelopabsb sbcom2 bitri vex
      wsbc opelcnv 3bitr4i eqrelriiv ) DEABCFZGZACBFZUAHACBIEJZDJZKUALZACUEQBUD
      QZUEUDKZUBLUHUCLUFABUDQCUEQUGABCEDMABECDNOUEUDUADPEPRACBDEMST $.
      $( [27-Aug-2011] $) $( [11-Dec-2003] $)
  $}

  ${
    $d x y $.
    $( The converse of the empty set. $)
    cnv0 $p |- `' (/) = (/) $=
      ( vx vy c0 ccnv relcnv rel0 cv cop wcel vex opelcnv noel 2false eqrelriiv
      bitr4i ) ABCDZCCEFAGZBGZHZPIRQHZCIZSCIZQRCAJBJKUBUASLTLMON $.
      $( [6-Apr-1998] $)

    $( The converse of the identity relation.  Theorem 3.7(ii) of [Monk1]
       p. 36.  (The proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    cnvi $p |- `' _I = _I $=
      ( vy vx cv cid wbr copab wceq ccnv ideq equcom bitri opabbii df-cnv df-id
      vex 3eqtr4i ) ACZBCZDEZBAFRQGZBAFDHDSTBASQRGTQRBOIABJKLBADMBANP $.
      $( [27-Aug-2011] $) $( [26-Apr-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The converse of a union is the union of converses.  Theorem 16 of
       [Suppes] p. 62.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    cnvun $p |- `' ( A u. B ) = ( `' A u. `' B ) $=
      ( vy vx cv wbr copab cun ccnv unopab brun opabbii eqtr4i uneq12i 3eqtr4ri
      wo df-cnv ) CEZDEZAFZDCGZRSBFZDCGZHZRSABHZFZDCGZAIZBIZHUEIUDTUBPZDCGUGTUB
      DCJUFUJDCRSABKLMUHUAUIUCDCAQDCBQNDCUEQO $.
      $( [27-Aug-2011] $) $( [25-Mar-1998] $)

    $( Distributive law for converse over intersection.  Theorem 15 of [Suppes]
       p. 62. $)
    cnvin $p |- `' ( A i^i B ) = ( `' A i^i `' B ) $=
      ( vx vy cin ccnv relcnv wrel relin1 ax-mp cv cop wcel wa elin vex opelcnv
      anbi12i bitr4i 3bitr4i eqrelriiv ) CDABEZFZAFZBFZEZUBGUDHUFHAGUDUEIJDKZCK
      ZLZUBMZUHUGLZUDMZUKUEMZNZUKUCMUKUFMUJUIAMZUIBMZNUNUIABOULUOUMUPUHUGACPZDP
      ZQUHUGBUQURQRSUHUGUBUQURQUKUDUEOTUA $.
      $( [25-Mar-1998] $)
  $}

  $( Distributive law for range over union.  Theorem 8 of [Suppes] p. 60. $)
  rnun $p |- ran ( A u. B ) = ( ran A u. ran B ) $=
    ( cun ccnv cdm crn cnvun dmeqi dmun eqtri df-rn uneq12i 3eqtr4i ) ABCZDZEZA
    DZEZBDZEZCZNFAFZBFZCPQSCZEUAOUDABGHQSIJNKUBRUCTAKBKLM $.
    $( [24-Mar-1998] $)

  $( The range of an intersection belongs the intersection of ranges.  Theorem
     9 of [Suppes] p. 60. $)
  rnin $p |- ran ( A i^i B ) C_ ( ran A i^i ran B ) $=
    ( cin ccnv cdm crn cnvin dmeqi dmin eqsstri df-rn ineq12i 3sstr4i ) ABCZDZE
    ZADZEZBDZEZCZNFAFZBFZCPQSCZEUAOUDABGHQSIJNKUBRUCTAKBKLM $.
    $( [15-Sep-2004] $)

  ${
    $d x y z A $.
    $( The range of a union.  Part of Exercise 8 of [Enderton] p. 41. $)
    rnuni $p |- ran U. A = U_ x e. A ran x $=
      ( vz vy cuni crn cv ciun cop wcel wex wrex eluni exbii excom ancom 19.41v
      wa elrn2 3bitr4i vex anbi2i 3bitri df-rex bitr4i eliun eqriv ) CBEZFZABAG
      ZFZHZDGCGZIZUHJZDKZUMUKJZABLZUMUIJUMULJUPUJBJZUQRZAKZURUPUNUJJZUSRZAKZDKV
      CDKZAKVAUOVDDAUNBMNVCDAOVEUTAVBDKZUSRUSVFRVEUTVFUSPVBUSDQUQVFUSDUMUJCUAZS
      UBTNUCUQABUDUEDUMUHVGSAUMBUKUFTUG $.
      $( [17-Mar-2004] $)
  $}

  $( Distributive law for image over union.  Theorem 35 of [Suppes] p. 65. $)
  imaundi $p |- ( A " ( B u. C ) ) = ( ( A " B ) u. ( A " C ) ) $=
    ( cun cres crn cima resundi rneqi rnun eqtri df-ima uneq12i 3eqtr4i ) ABCDZ
    EZFZABEZFZACEZFZDZAOGABGZACGZDQRTDZFUBPUEABCHIRTJKAOLUCSUDUAABLACLMN $.
    $( [30-Sep-2002] $)

  $( The image of a union.  (Contributed by Jeff Hoffman, 17-Feb-2008.) $)
  imaundir $p |- ( ( A u. B ) " C ) = ( ( A " C ) u. ( B " C ) ) $=
    ( cun cima cres crn df-ima resundir rneqi rnun 3eqtri uneq12i eqtr4i ) ABDZ
    CEZACFZGZBCFZGZDZACEZBCEZDPOCFZGQSDZGUAOCHUDUEABCIJQSKLUBRUCTACHBCHMN $.
    $( [17-Feb-2008] $)

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( An upper bound for intersection with a domain.  Theorem 40 of [Suppes]
       p. 66, who calls it "somewhat surprising." $)
    dminss $p |- ( dom R i^i A ) C_ ( `' R " ( R " A ) ) $=
      ( vx vy cdm cin ccnv cima cv wbr wcel wa 19.8a ancoms elima2 sylibr simpl
      wex vex brcnv jca eximi eldm anbi1i elin 19.41v 3bitr4i 3imtr4i ssriv ) C
      BEZAFZBGZBAHZHZCIZDIZBJZUOAKZLZDRZUPUMKZUPUOULJZLZDRUOUKKZUOUNKUSVCDUSVAV
      BUSURUQLZCRZVAURUQVFVECMNCUPBADSZOPUSUQVBUQURQUPUOBVGCSZTPUAUBUOUJKZURLUQ
      DRZURLVDUTVIVJURDUOBVHUCUDUOUJAUEUQURDUFUGDUOULUMVHOUHUI $.
      $( [11-Aug-2004] $)

    $( An upper bound for intersection with an image.  Theorem 41 of [Suppes]
       p. 66. $)
    imainss $p |- ( ( R " A ) i^i B ) C_ ( R " ( A i^i ( `' R " B ) ) ) $=
      ( vy vx cima cin ccnv cv wcel wbr wa wex brcnv 19.8a sylan2br elin elima2
      vex anbi1i ancoms anim2i simprl anassrs anbi2i bitri sylibr eximi 3bitr4i
      jca 19.41v 3imtr4i ssriv ) DCAFZBGZCACHZBFZGZFZEIZAJZUTDIZCKZLZVBBJZLZEMZ
      UTURJZVCLZEMVBUOJZVBUSJVFVIEVFVAVEVBUTUPKZLZDMZLZVCLZVIVAVCVEVOVAVCVELZLV
      NVCVPVMVAVEVCVMVCVEVKVMVBUTCDSZESZNVLDOPUAUBVAVCVEUCUJUDVHVNVCVHVAUTUQJZL
      VNUTAUQQVSVMVADUTUPBVRRUEUFTUGUHVBUNJZVELVDEMZVELVJVGVTWAVEEVBCAVQRTVBUNB
      QVDVEEUKUIEVBCURVQRULUM $.
      $( [11-Aug-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The converse of a cross product.  Exercise 11 of [Suppes] p. 67.  (The
       proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    cnvxp $p |- `' ( A X. B ) = ( B X. A ) $=
      ( vy vx cv wcel copab ccnv cxp cnvopab ancom opabbii eqtri cnveqi 3eqtr4i
      wa df-xp ) CEAFZDEBFZPZCDGZHZSRPZDCGZABIZHBAIUBTDCGUDTCDJTUCDCRSKLMUEUACD
      ABQNDCBAQO $.
      $( [27-Aug-2011] $) $( [14-Aug-1999] $)
  $}

  $( The cross product with the empty set is empty.  Part of Theorem 3.13(ii)
     of [Monk1] p. 37. $)
  xp0 $p |- ( A X. (/) ) = (/) $=
    ( c0 cxp ccnv xp0r cnveqi cnvxp cnv0 3eqtr3i ) BACZDBDABCBJBAEFBAGHI $.
    $( [12-Apr-2004] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( The cross product of nonempty classes is nonempty.  (Variation of a
       theorem contributed by Raph Levien, 30-Jun-2006.) $)
    xpnz $p |- ( ( A =/= (/) /\ B =/= (/) ) <-> ( A X. B ) =/= (/) ) $=
      ( vx vy vz c0 wne wa cxp cv wcel wex anbi12i eeanv bitr4i cop wceq syl6eq
      n0 necon3i opex eleq1 vex opelxp syl6bb cla4ev sylibr exlimivv sylbi xp0r
      xpeq1 xpeq2 xp0 jca impbii ) AFGZBFGZHZABIZFGZURCJZAKZDJZBKZHZDLCLZUTURVB
      CLZVDDLZHVFUPVGUQVHCASDBSMVBVDCDNOVEUTCDVEEJZUSKZELUTVJVEEVAVCPZVAVCUAVIV
      KQVJVKUSKVEVIVKUSUBVAVCABDUCUDUEUFEUSSUGUHUIUTUPUQAFUSFAFQUSFBIFAFBUKBUJR
      TBFUSFBFQUSAFIFBFAULAUMRTUNUO $.
      $( [19-Apr-2007] $) $( [30-Jun-2006] $)
  $}

  $( At least one member of an empty cross product is empty. $)
  xpeq0 $p |- ( ( A X. B ) = (/) <-> ( A = (/) \/ B = (/) ) ) $=
    ( cxp c0 wceq wne wa wn wo xpnz necon2bbii ianor nne orbi12i 3bitri ) ABCZD
    EADFZBDFZGZHQHZRHZIADEZBDEZISPDABJKQRLTUBUAUCADMBDMNO $.
    $( [27-Aug-2006] $)

  $( Cross products with disjoint sets are disjoint. $)
  xpdisj1 $p |- ( ( A i^i B ) = (/) -> ( ( A X. C ) i^i ( B X. D ) ) = (/) ) $=
    ( cin c0 wceq cxp inxp xpeq1 xp0r syl6eq syl5eq ) ABEZFGZACHBDHENCDEZHZFACB
    DIOQFPHFNFPJPKLM $.
    $( [13-Sep-2004] $)

  $( Cross products with disjoint sets are disjoint. $)
  xpdisj2 $p |- ( ( A i^i B ) = (/) -> ( ( C X. A ) i^i ( D X. B ) ) = (/) ) $=
    ( cin c0 wceq cxp inxp xpeq2 xp0 syl6eq syl5eq ) ABEZFGZCAHDBHECDEZNHZFCADB
    IOQPFHFNFPJPKLM $.
    $( [13-Sep-2004] $)

  $( Cross products with two different singletons are disjoint. $)
  xpsndisj $p |- ( B =/= D -> ( ( A X. { B } ) i^i ( C X. { D } ) ) = (/) ) $=
    ( wne csn cin c0 wceq cxp disjsn2 xpdisj2 syl ) BDEBFZDFZGHIANJCOJGHIBDKNOA
    CLM $.
    $( [3-Jun-2007] $) $( [28-Jul-2004] $)

  $( A double restriction to disjoint classes is the empty set.  (The proof was
     shortened by Andrew Salmon, 27-Aug-2011.) $)
  resdisj $p |- ( ( A i^i B ) = (/) -> ( ( C |` A ) |` B ) = (/) ) $=
    ( cin c0 wceq cres resres reseq2 res0 syl6eq syl5eq ) ABDZEFZCAGBGCMGZECABH
    NOCEGEMECICJKL $.
    $( [27-Aug-2011] $) $( [7-Oct-2004] $)

  $( The range of a cross product.  Part of Theorem 3.13(x) of [Monk1]
     p. 37. $)
  rnxp $p |- ( A =/= (/) -> ran ( A X. B ) = B ) $=
    ( c0 wne cxp crn cdm ccnv df-rn cnvxp dmeqi eqtri dmxp syl5eq ) ACDABEZFZBA
    EZGZBPOHZGROISQABJKLBAMN $.
    $( [9-Apr-2007] $) $( [12-Apr-2004] $)

  $( The domain of a cross product is a subclass of the first factor. $)
  dmxpss $p |- dom ( A X. B ) C_ A $=
    ( cxp cdm wss c0 wceq 0ss xpeq2 xp0 syl6eq dmeqd dm0 sseq1d mpbiri wne dmxp
    eqimss syl pm2.61ine ) ABCZDZAEZBFBFGZUCFAEAHUDUBFAUDUBFDFUDUAFUDUAAFCFBFAI
    AJKLMKNOBFPUBAGUCABQUBARST $.
    $( [19-Mar-2007] $)

  $( The range of a cross product is a subclass of the second factor.  (The
     proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
  rnxpss $p |- ran ( A X. B ) C_ B $=
    ( cxp crn ccnv cdm df-rn cnvxp dmeqi dmxpss eqsstri ) ABCZDLEZFZBLGNBACZFBM
    OABHIBAJKK $.
    $( [27-Aug-2011] $) $( [16-Jan-2006] $)

  $( A cross-product subclass relationship is equivalent to the relationship
     for it components. $)
  ssxpb $p |- ( ( A X. B ) =/= (/) -> ( ( A X. B ) C_ ( C X. D ) <->
              ( A C_ C /\ B C_ D ) ) ) $=
    ( cxp c0 wne wss wa cdm wceq xpnz dmxp adantl sylbir adantr eqsstr3d syl6ss
    dmss crn dmxpss rnxp rnss rnxpss jca ex xpss12 impbid1 ) ABEZFGZUICDEZHZACH
    ZBDHZIZUJULUOUJULIZUMUNUPAUKJZCUPAUIJZUQUJURAKZULUJAFGZBFGZIZUSABLZVAUSUTAB
    MNOPULURUQHUJUIUKSNQCDUARUPBUKTZDUPBUITZVDUJVEBKZULUJVBVFVCUTVFVAABUBPOPULV
    EVDHUJUIUKUCNQCDUDRUEUFACBDUGUH $.
    $( [17-Dec-2008] $)

  $( The cross product of non-empty classes is one-to-one. $)
  xp11 $p |- ( ( A =/= (/) /\ B =/= (/) )
      -> ( ( A X. B ) = ( C X. D ) <-> ( A = C /\ B = D ) ) ) $=
    ( c0 wne wa cxp wceq wi xpnz anidm neeq1 anbi2d syl5bbr wss ssxpb syl5ibcom
    eqimss eqss eqimss2 anim12d an4 anbi12i bitr4i syl6ib sylbid com12 sylan9eq
    sylbi xpeq1 xpeq2 impbid1 ) AEFBEFGZABHZCDHZIZACIZBDIZGZUNUOEFZUQUTJABKUQVA
    UTUQVAVAUPEFZGZUTVAVAVAGUQVCVALUQVAVBVAUOUPEMNOUQVCACPZBDPZGZCAPZDBPZGZGZUT
    UQVAVFVBVIUQUOUPPVAVFUOUPSABCDQRUQUPUOPVBVIUPUOUACDABQRUBVJVDVGGZVEVHGZGUTV
    DVEVGVHUCURVKUSVLACTBDTUDUEUFUGUHUJURUSUOCBHUPACBUKBDCULUIUM $.
    $( [31-May-2008] $)

  $( Cancellation law for cross-product. $)
  xpcan $p |- ( C =/= (/) -> ( ( C X. A ) = ( C X. B ) <-> A = B ) ) $=
    ( c0 wne cxp wceq wb wa xp11 biantrur syl6bbr wn wi nne xpeq2 syl6eq eqeq1d
    eqid xp0 eqcom syl6bb adantl df-ne wo xpeq0 orel1 syl5bi sylbi adantr simpr
    sylbid jctild eqtr3 syl6 sylan2b impbid1 pm2.61dan ) CDEZADEZCAFZCBFZGZABGZ
    HUSUTIVCCCGZVDIVDCACBJVEVDCSKLUSUTMZIVCVDVFUSADGZVCVDNADOUSVGIZVCVGBDGZIVDV
    HVCVIVGVHVCVBDGZVIVGVCVJHUSVGVCDVBGVJVGVADVBVGVACDFDADCPCTQRDVBUAUBUCUSVJVI
    NZVGUSCDGZMZVKCDUDVJVLVIUEVMVICBUFVLVIUGUHUIUJULUSVGUKUMABDUNUOUPABCPUQUR
    $.
    $( [30-Aug-2011] $)

  $( The second argument of a cross product is one-to-one. $)
  xp11bOLD $p |- ( A =/= (/) -> ( ( A X. A ) = ( A X. B ) <-> A = B ) ) $=
    ( c0 wne cxp wceq wb wa xp11 eqid biantrur syl6bbr anidms ) ACDZAAEABEFZABF
    ZGNNHOAAFZPHPAAABIQPAJKLM $.
    $( [31-May-2008] $)

  $( Cancellation law for cross-product. $)
  xpcan2 $p |- ( C =/= (/) -> ( ( A X. C ) = ( B X. C ) <-> A = B ) ) $=
    ( c0 wne cxp wceq wb wa xp11 eqid biantru syl6bbr wn nne xp0r syl6eq eqeq1d
    xpeq1 eqcom syl6bb adantr wi df-ne wo xpeq0 orel2 syl5bi sylbi adantl simpl
    sylbid jctild eqtr3 syl6 impbid1 sylanb pm2.61ian ) ADEZCDEZACFZBCFZGZABGZH
    ZUSUTIVCVDCCGZIVDACBCJVFVDCKLMUSNADGZUTVEADOVGUTIZVCVDVHVCVGBDGZIVDVHVCVIVG
    VHVCVBDGZVIVGVCVJHUTVGVCDVBGVJVGVADVBVGVADCFDADCSCPQRDVBTUAUBUTVJVIUCZVGUTC
    DGZNZVKCDUDVJVIVLUEVMVIBCUFVLVIUGUHUIUJULVGUTUKUMABDUNUOABCSUPUQUR $.
    $( [30-Aug-2011] $)

  $( The first argument of a cross product is one-to-one. $)
  xp11aOLD $p |- ( A =/= (/) -> ( ( A X. A ) = ( B X. A ) <-> A = B ) ) $=
    ( c0 wne cxp wceq wb wa xp11 eqid biantru syl6bbr anidms ) ACDZAAEBAEFZABFZ
    GNNHOPAAFZHPAABAIQPAJKLM $.
    $( [31-May-2008] $)

  $( If a cross product is a set, one of its components must be a set. $)
  xpexr $p |- ( ( A X. B ) e. C -> ( A e. _V \/ B e. _V ) ) $=
    ( cxp wcel cvv wn wi wceq 0ex eleq1 mpbiri pm2.24d a1d wne crn rnexg eleq1d
    c0 rnxp syl5ib a1dd pm2.61ine orrd ) ABDZCEZAFEZBFEZUFUGGZUHHZHASASIZUJUFUK
    UGUHUKUGSFEJASFKLMNASOZUFUHUIUFUEPZFEULUHUECQULUMBFABTRUAUBUCUD $.
    $( [27-Aug-2006] $)

  $( If a nonempty cross product is a set, so are both of its components. $)
  xpexr2 $p |- ( ( ( A X. B ) e. C /\ ( A X. B ) =/= (/) ) ->
               ( A e. _V /\ B e. _V ) ) $=
    ( cxp c0 wne wcel wa cvv xpnz cdm wceq dmxp adantl adantr eqeltrrd crn rnxp
    dmexg rnexg anim12i anandis ancom2s sylan2br ) ABDZEFUECGZAEFZBEFZHAIGZBIGZ
    HZABJUFUHUGUKUFUHUGUKUFUHHZUIUFUGHZUJULUEKZAIUHUNALUFABMNUFUNIGUHUECSOPUMUE
    QZBIUGUOBLUFABRNUFUOIGUGUECTOPUAUBUCUD $.
    $( [5-May-2007] $) $( [27-Aug-2006] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Subset of the range of a restriction. $)
    ssrnres $p |- ( B C_ ran ( C |` A ) <-> ran ( C i^i ( A X. B ) ) = B ) $=
      ( vy vx cxp cin crn wceq wss wa cres rnss ax-mp cvv wcel wex elrn2 3bitri
      cv eqss inss2 rnxpss sstri biantrur ssid ssv xpss12 mp2an df-res sseqtr4i
      sslin sstr mpan2 cop ssel syl6ib ancrd opelxp anbi2i opelres anbi1i anass
      vex elin bitr2i exbii 19.41v syl6ibr ssrdv impbii 3bitr2ri ) CABFZGZHZBIV
      OBJZBVOJZKVQBCALZHZJZVOBUAVPVQVOVMHZBVNVMJVOWAJCVMUBVNVMMNABUCUDUEVQVTVQV
      OVSJZVTVNVRJWBVNCAOFZGZVRVMWCJZVNWDJAAJBOJWEAUFBUGAABOUHUIVMWCCULNCAUJUKV
      NVRMNBVOVSUMUNVTDBVOVTDTZBPZETZWFUOZVRPZEQZWGKZWFVOPZVTWGWKVTWGWFVSPWKBVS
      WFUPEWFVRDVDZRUQURWMWIVNPZEQWJWGKZEQWLEWFVNWNRWOWPEWOWICPZWIVMPZKWQWHAPZW
      GKZKZWPWICVMVEWRWTWQWHWFABWNUSUTWPWQWSKZWGKXAWJXBWGWHWFCAWNVAVBWQWSWGVCVF
      SVGWJWGEVHSVIVJVKVL $.
      $( [16-Jan-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.
    $( Range of the intersection with a cross product.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    rninxp $p |- ( ran ( C i^i ( A X. B ) ) = B <->
                 A. y e. B E. x e. A x C y ) $=
      ( cres crn wss wcel wral cxp cin wceq wbr wrex dfss3 ssrnres cima df-ima
      cv eleq2i vex elima bitr3i ralbii 3bitr3i ) DECFGZHBTZUGIZBDJECDKLGDMATUH
      ENACOZBDJBDUGPCDEQUIUJBDUIUHECRZIUJUKUGUHECSUAAUHECBUBUCUDUEUF $.
      $( [27-Aug-2011] $) $( [17-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x y C $.
    $( Domain of the intersection with a cross product. $)
    dminxp $p |- ( dom ( C i^i ( A X. B ) ) = A <->
                 A. x e. A E. y e. B x C y ) $=
      ( cxp cin cdm wceq ccnv crn cv wbr wrex wral dfdm4 cnvin cnvxp eqtri vex
      ineq2i rneqi eqeq1i rninxp brcnv rexbii ralbii 3bitri ) ECDFZGZHZCIEJZDCF
      ZGZKZCIBLZALZULMZBDNZACOUQUPEMZBDNZACOUKUOCUKUJJZKUOUJPVBUNVBULUIJZGUNEUI
      QVCUMULCDRUASUBSUCBADCULUDUSVAACURUTBDUPUQEBTATUEUFUGUH $.
      $( [17-Jan-2006] $)
  $}

  ${
    $d x y R $.
    $( Alternate definition of relation.  Exercise 2 of [TakeutiZaring]
       p. 25. $)
    dfrel2 $p |- ( Rel R <-> `' `' R = R ) $=
      ( vx vy wrel ccnv wceq relcnv cv cop wcel vex opelcnv bitri eqrelriv mpan
      releq mpbii impbii ) ADZAEZEZAFZUADZSUBTGZBCUAABHZCHZIZUAJUFUEITJUGAJUEUF
      TBKZCKZLUFUEAUIUHLMNOUBUCSUDUAAPQR $.
      $( [15-Aug-2004] $) $( [29-Dec-1996] $)
  $}

  $( Equality theorem for converse.  (Contributed by FL, 19-Sep-2011.) $)
  cnveqb $p |- ( ( Rel A /\ Rel B ) -> ( A = B <-> `' A = `' B ) ) $=
    ( wrel wa wceq ccnv cnveq wi dfrel2 eqeq2 syl5ibr eqcoms sylbi eqeq1 imbi2d
    imp impbid2 ) ACZBCZDABEZAFZBFZEZABGRSUCTHZRUAFZAESUDHZAIUFAUESUDAUEEZUCUEB
    EZHZSUBFZBEUIBIUIBUJUCUHBUJEUEUJEUAUBGBUJUEJKLMUGTUHUCAUEBNOKLMPQ $.
    $( [19-Sep-2011] $)

  $( The double converse of a class strips out all elements that are not
     ordered pairs. $)
  cnvcnv $p |- `' `' A = ( A i^i ( _V X. _V ) ) $=
    ( cvv cxp ccnv cnvin cnveqi wrel wceq inss2 df-rel mpbir dfrel2 mpbi relcnv
    cin wss relxp sseqtr4i dfss eqtr4i 3eqtr3ri ) ABBCZOZDZDZADZUBDZOZDZUCUFDZU
    DUHAUBEFUCGZUEUCHUKUCUBPAUBIUCJKUCLMUIUJUGDZOZUJUFUGEUJULPUJUMHUJUBULUJGUJU
    BPUFNUJJMUBGULUBHBBQUBLMRUJULSMTUA $.
    $( [8-Dec-2003] $)

  $( The double converse of a class equals its restriction to the universe. $)
  cnvcnv2 $p |- `' `' A = ( A |` _V ) $=
    ( ccnv cvv cxp cin cres cnvcnv df-res eqtr4i ) ABBACCDEACFAGACHI $.
    $( [8-Oct-2007] $)

  $( The double converse of a class is a subclass.  Exercise 2 of
     [TakeutiZaring] p. 25. $)
  cnvcnvss $p |- `' `' A C_ A $=
    ( ccnv cvv cxp cin cnvcnv inss1 eqsstri ) ABBACCDZEAAFAIGH $.
    $( [23-Jul-2004] $)
  ${
    $d x y A $.
    $( The domain of a singleton is nonzero iff the singleton argument is an
       ordered pair.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    dmsnn0 $p |- ( A e. ( _V X. _V ) <-> dom { A } =/= (/) ) $=
      ( vx vy cv cop wceq wex csn cdm wcel cvv cxp wne wbr vex eldm df-br exbii
      c0 opex elsnc eqcom 3bitri bitr2i elvv n0 3bitr4i ) ABDZCDZEZFZCGZBGUHAHZ
      IZJZBGAKKLJUNSMULUOBUOUHUIUMNZCGULCUHUMBOPUPUKCUPUJUMJUJAFUKUHUIUMQUJAUHU
      ITUAUJAUBUCRUDRBCAUEBUNUFUG $.
      $( [27-Aug-2011] $) $( [14-Dec-2008] $)
  $}

  $( The range of a singleton is nonzero iff the singleton argument is an
     ordered pair. $)
  rnsnn0 $p |- ( A e. ( _V X. _V ) <-> ran { A } =/= (/) ) $=
    ( cvv cxp wcel csn cdm c0 wne crn dmsnn0 dm0rn0 necon3bii bitri ) ABBCDAEZF
    ZGHNIZGHAJOGPGNKLM $.
    $( [14-Dec-2008] $)

  $( The domain of the singleton of the empty set is empty. $)
  dmsn0 $p |- dom { (/) } = (/) $=
    ( c0 csn cdm wceq cvv cxp wcel wn 0nelxp dmsnn0 necon2bbii mpbir ) ABCZADAE
    EFGZHEEINMAAJKL $.
    $( [17-Dec-2008] $) $( [30-Jan-2004] $)

  $( The domain of a singleton is empty if the singleton's argument contains
     the empty set. $)
  dmsn0el $p |- ( (/) e. A -> dom { A } = (/) ) $=
    ( c0 wcel csn cdm wne cvv cxp wn dmsnn0 0nelelxp sylbir necon4ai ) BACZADEZ
    BOBFAGGHCNIAJGGAKLM $.
    $( [15-Dec-2008] $)

  ${
    relsn2.1 $e |- A e. _V $.
    $( A singleton is a relation iff it has a nonempty domain. $)
    relsn2 $p |- ( Rel { A } <-> dom { A } =/= (/) ) $=
      ( csn wrel cvv cxp wcel cdm c0 wne relsn dmsnn0 bitri ) ACZDAEEFGNHIJABKA
      LM $.
      $( [25-Sep-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The domain of a singleton of an ordered pair is the singleton of the
       first member.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    dmsnop $p |- dom { <. A , B >. } = { A } $=
      ( vx vy cop csn cdm wcel wex wceq opex elsnc exbii vex opth1 opeq2 eqeq2d
      cv eximi exbidv exlimiv cvv isset sylbi wn a9e ax-mp opprc2 pm2.61i opeq1
      mpbiri mpbii impbii bitri eldm2 elsn 3bitr4i eqriv ) CABEZFZGZAFZCRZDRZEZ
      UTHZDIZVCAJZVCVAHVCVBHVGVEUSJZDIZVHVFVIDVEUSVCVDKLMVJVHVIVHDVCVDABCNZOUAV
      HVEVCBEZJZDIZVJBUBHZVNVOVDBJZDIVNDBUCVPVMDVDBVCPSUDVOUEZVNVEVCVCEZJZDIZVD
      VCJZDIVTDCUFWAVSDVDVCVCPSUGVQVMVSDVQVLVRVEVCBUHQTUKUIVHVMVIDVHVLUSVEVCABU
      JQTULUMUNDVCUTVKUOCAUPUQUR $.
      $( [27-Aug-2011] $) $( [30-Jan-2004] $)
  $}

  $( The domain of an unordered pair of ordered pairs. $)
  dmprop $p |- dom { <. A , C >. , <. B , D >. } = { A , B } $=
    ( cop cpr cdm csn cun df-pr dmeqi dmsnop uneq12i dmun 3eqtr4i eqtri ) ACEZB
    DEZFZGQHZRHZIZGZABFZSUBQRJKTGZUAGZIAHZBHZIUCUDUEUGUFUHACLBDLMTUANABJOP $.
    $( [13-Sep-2011] $)

  $( The domain of an unordered triple of ordered pairs. $)
  dmtpop $p |- dom { <. A , D >. , <. B , E >. , <. C , F >. }
                      = { A , B , C } $=
    ( cop ctp cdm cpr csn df-tp dmeqi dmun dmprop dmsnop uneq12i 3eqtri eqtr4i
    cun ) ADGZBEGZCFGZHZIZABJZCKZTZABCHUEUAUBJZUCKZTZIUIIZUJIZTUHUDUKUAUBUCLMUI
    UJNULUFUMUGABDEOCFPQRABCLS $.
    $( [14-Sep-2011] $)

  $( The domain of the singleton of the singleton of a singleton. $)
  dmsnsnsn $p |- dom { { { A } } } = { A } $=
    ( cop csn cdm cpr dfsn2 preq2i df-op 3eqtr4ri sneqi dmeqi dmsnop eqtr3i ) A
    ABZCZDACZCZCZDPORNQPPEPAAEZEQNPSPAFGPFAAHIJKAALM $.
    $( [15-Sep-2004] $)

  ${
    op1sta.1 $e |- A e. _V $.
    $( Extract the first member of an ordered pair.  (See ~ op2nda to extract
       the second member, ~ op1stb for an alternate version, and ~ op1st for
       the preferred version..)  (Contributed by Raph Levien, 4-Dec-2003.) $)
    op1sta $p |- U. dom { <. A , B >. } = A $=
      ( cop csn cdm cuni dmsnop unieqi unisn eqtri ) ABDEFZGAEZGALMABHIACJK $.
      $( [4-Dec-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    cnvsn.1 $e |- A e. _V $.
    cnvsn.2 $e |- B e. _V $.
    $( Converse of a singleton of an ordered pair. $)
    cnvsn $p |- `' { <. A , B >. } = { <. B , A >. } $=
      ( vy vx cop csn ccnv relcnv relsnop cv wceq wa wcel ancom opex elsnc opth
      vex bitri opelcnv 3bitri 3bitr4ri eqrelriiv ) EFABGZHZIZBAGZHZUGJBADKELZB
      MZFLZAMZNZUNULNZUKUMGZUJOZUQUHOZULUNPURUQUIMUOUQUIUKUMQRUKUMBAETZFTZCSUAU
      SUMUKGZUGOVBUFMUPUKUMUGUTVAUBVBUFUMUKQRUMUKABVAUTDSUCUDUE $.
      $( [11-May-1998] $)

    $( Swap the members of an ordered pair. $)
    opswap $p |- U. `' { <. A , B >. } = <. B , A >. $=
      ( cop csn ccnv cuni cnvsn unieqi opex unisn eqtri ) ABEFGZHBAEZFZHONPABCD
      IJOBAKLM $.
      $( [14-Dec-2008] $)

    $( The range of a singleton of an ordered pair is the singleton of the
       second member. $)
    rnsnop $p |- ran { <. A , B >. } = { B } $=
      ( cop csn crn ccnv cdm df-rn cnvsn dmeqi dmsnop 3eqtri ) ABEFZGOHZIBAEFZI
      BFOJPQABCDKLBAMN $.
      $( [24-Jul-2004] $)

    $( Extract the second member of an ordered pair.  Theorem 5.12(ii) of
       [Monk1] p. 52.  (See ~ op1stb to extract the first member, ~ op2nda for
       an alternate version, and ~ op2nd for the preferred version.) $)
    op2ndb $p |- |^| |^| |^| `' { <. A , B >. } = B $=
      ( cop csn ccnv cint cnvsn inteqi opex intsn eqtri op1stb ) ABEFGZHZHZHBAE
      ZHZHBQSPRPRFZHROTABCDIJRBAKLMJJBADNM $.
      $( [25-Nov-2003] $)

    $( Extract the second member of an ordered pair.  (See ~ op1sta to extract
       the first member, ~ op2ndb for an alternate version, and ~ op2nd for the
       preferred version.)  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    op2nda $p |- U. ran { <. A , B >. } = B $=
      ( cop csn crn cuni rnsnop unieqi unisn eqtri ) ABEFGZHBFZHBMNABCDIJBDKL
      $.
      $( [27-Aug-2011] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in a cross product.  This version requires no quantifiers or
       dummy variables.  See also ~ elxp5 , ~ elxp6 , and ~ elxp7 . $)
    elxp4 $p |- ( A e. ( B X. C ) <-> ( A = <. U. dom { A } , U. ran { A } >.
                 /\ ( U. dom { A } e. B /\ U. ran { A } e. C ) ) ) $=
      ( vx vy wcel cv cop wceq wa wex csn cdm cuni crn sneq unieqd vex pm4.71ri
      syl6req elxp rneqd op2nda anbi1i anass bitri exbii snex rnex uniex eqeq2d
      opeq2 eleq1 anbi2d anbi12d ceqsexv dmeqd op1sta 3bitri dmex opeq1 anbi1d
      cxp ) ABCVCFADGZEGZHZIZVDBFZVECFZJZJZEKZDKVDALZMZNZIZAVDVMOZNZHZIZVHVRCFZ
      JZJZJZDKAVOVRHZIZVOBFZWAJZJZDEABCUAVLWDDVLWCVPVTJZWBJWDVLVEVRIZVKJZEKWCVK
      WLEVKWKVGJZVJJWLVGWMVJVGWKVGVRVFLZOZNVEVGVQWOVGVMWNAVFPUBQVDVEDRZERUCTSUD
      WKVGVJUEUFUGVKWCEVRVQVMAUHZUIUJWKVGVTVJWBWKVFVSAVEVRVDULUKWKVIWAVHVEVRCUM
      UNUOUPUFVTWJWBVTVPVTVOVSLZMZNVDVTVNWSVTVMWRAVSPUQQVDVRWPURTSUDVPVTWBUEUSU
      GWCWIDVOVNVMWQUTUJVPVTWFWBWHVPVSWEAVDVOVRVAUKVPVHWGWAVDVOBUMVBUOUPUS $.
      $( [17-Feb-2004] $)

    $( Membership in a cross product requiring no quantifiers or dummy
       variables.  Provides a slightly shorter version of ~ elxp4 when the
       double intersection does not create class existence problems (caused by
       ~ int0 ). $)
    elxp5 $p |- ( A e. ( B X. C ) <-> ( A = <. |^| |^| A , U. ran { A } >.
                 /\ ( |^| |^| A e. B /\ U. ran { A } e. C ) ) ) $=
      ( vx vy wcel cv cop wceq wex cint csn crn cuni vex syl6req pm4.71ri eleq1
      wa cvv elxp sneq rneqd unieqd op2nda anbi1i anass bitri exbii uniex opeq2
      snex rnex eqeq2d anbi2d anbi12d ceqsexv inteq inteqd op1stb 3bitri adantr
      cxp mpbii exlimiv elex ad2antrl opeq1 anbi1d ceqsexgv pm5.21nii ) ABCVCFA
      DGZEGZHZIZVLBFZVMCFZSZSZEJZDJVLAKZKZIZAVLALZMZNZHZIZVPWFCFZSZSZSZDJZAWBWF
      HZIZWBBFZWISZSZDEABCUAVTWLDVTWKWCWHSZWJSWLVTVMWFIZVSSZEJWKVSXAEVSWTVOSZVR
      SXAVOXBVRVOWTVOWFVNLZMZNVMVOWEXDVOWDXCAVNUBUCUDVLVMDOZEOUEPQUFWTVOVRUGUHU
      IVSWKEWFWEWDAULUMUJWTVOWHVRWJWTVNWGAVMWFVLUKUNWTVQWIVPVMWFCRUOUPUQUHWHWSW
      JWHWCWHWBWGKZKVLWHWAXFAWGURUSVLWFXEUTPQUFWCWHWJUGVAUIWMWBTFZWRWLXGDWCXGWK
      WCVLTFXGXEVLWBTRVDVBVEWPXGWOWIWBBVFVGWKWRDWBTWCWHWOWJWQWCWGWNAVLWBWFVHUNW
      CVPWPWIVLWBBRVIUPVJVKVA $.
      $( [1-Aug-2004] $)
  $}

  $( Alternate definition of relation. $)
  dfrel3 $p |- ( Rel R <-> ( R |` _V ) = R ) $=
    ( wrel ccnv wceq cvv cres dfrel2 cnvcnv2 eqeq1i bitri ) ABACCZADAEFZADAGKLA
    AHIJ $.
    $( [14-May-2008] $)

  $( The domain of a universal restriction. $)
  dmresv $p |- dom ( A |` _V ) = dom A $=
    ( cvv cres cdm cin dmres incom inv1 3eqtri ) ABCDBADZEJBEJABFBJGJHI $.
    $( [14-May-2008] $)

  $( The range of a universal restriction. $)
  rnresv $p |- ran ( A |` _V ) = ran A $=
    ( ccnv crn cvv cres cnvcnv2 rneqi rncnvcnv eqtr3i ) ABBZCADEZCACJKAFGAHI $.
    $( [14-May-2008] $)

  $( Range defined in terms of image. $)
  dfrn4 $p |- ran A = ( A " _V ) $=
    ( cvv cima cres crn df-ima rnresv eqtr2i ) ABCABDEAEABFAGH $.
    $( [14-May-2008] $)

  $( The restriction of the double converse of a class.  (The proof was
     shortened by Andrew Salmon, 27-Aug-2011.) $)
  rescnvcnv $p |- ( `' `' A |` B ) = ( A |` B ) $=
    ( ccnv cres cvv cin wceq cnvcnv2 reseq1 resres wss ssv sseqin2 mpbi reseq2i
    ax-mp 3eqtri ) ACCZBDZAEDZBDZAEBFZDABDRTGSUAGAHRTBIPAEBJUBBABEKUBBGBLBEMNOQ
    $.
    $( [27-Aug-2011] $) $( [8-Apr-2007] $)

  $( The double converse of the restriction of a class. $)
  cnvcnvres $p |- `' `' ( A |` B ) = ( `' `' A |` B ) $=
    ( cres ccnv wrel wceq relres dfrel2 mpbi rescnvcnv eqtr4i ) ABCZDDZLADDBCLE
    MLFABGLHIABJK $.
    $( [3-Jun-2007] $)

  $( The image of the double converse of a class. $)
  imacnvcnv $p |- ( `' `' A " B ) = ( A " B ) $=
    ( ccnv cres crn cima rescnvcnv rneqi df-ima 3eqtr4i ) ACCZBDZEABDZEKBFABFLM
    ABGHKBIABIJ $.
    $( [8-Apr-2007] $)

  ${
    $d s t A $.  $d s t B $.  $d s t F $.
    $( An image under the converse of a restriction.  (Contributed by Jeff
       Hankins, 12-Jul-2009.) $)
    cnvresima $p |- ( `' ( F |` A ) " B ) = ( ( `' F " B ) i^i A ) $=
      ( vt vs cres ccnv cima cin wcel cop wex vex elima3 opelres opelcnv anbi1i
      cv wa 3bitr4i anbi2i anass bitr4i exbii 19.41v bitri bitr2i 3bitri eqriv
      elin ) DCAFZGZBHZCGZBHZAIZDRZUMJERZBJZURUQKZULJZSZELZUSUTUNJZSZELZUQAJZSZ
      UQUPJZEUQULBDMZNVCVEVGSZELVHVBVKEVBUSVDVGSZSVKVAVLUSUQURKZUKJVMCJZVGSVAVL
      UQURCAEMZOURUQUKVOVJPVDVNVGURUQCVOVJPQTUAUSVDVGUBUCUDVEVGEUEUFVIUQUOJZVGS
      VHUQUOAUJVPVFVGEUQUNBVJNQUGUHUI $.
      $( [12-Jul-2009] $)
  $}

  $( A class restricted to its domain equals its double converse. $)
  resdm2 $p |- ( A |` dom A ) = `' `' A $=
    ( ccnv cdm cres rescnvcnv wrel relcnv resdm ax-mp dmcnvcnv reseq2i 3eqtr3ri
    wceq ) ABZBZOCZDZAPDOAACZDAPEOFQOMNGOHIPRAAJKL $.
    $( [8-Apr-2007] $)

  $( Restriction to the domain of a restriction. $)
  resdmres $p |- ( A |` dom ( A |` B ) ) = ( A |` B ) $=
    ( cres cdm ccnv cvv cxp in12 df-res resdm2 eqtr3i ineq2i incom 3eqtri dmres
    cin xpeq1i xpindir eqtri 3eqtr4i rescnvcnv ) AABCZDZCZAEEZBCZUBABFGZADZFGZP
    ZPZUEUGPZUDUFUKUGAUIPZPUGUEPULAUGUIHUMUEUGAUHCUMUEAUHIAJKLUGUEMNUDAUCFGZPUK
    AUCIUNUJAUNBUHPZFGUJUCUOFABOQBUHFRSLSUEBITABUAS $.
    $( [8-Apr-2007] $)

  $( The image of the domain of a restriction. $)
  imadmres $p |- ( A " dom ( A |` B ) ) = ( A " B ) $=
    ( cres cdm crn cima resdmres rneqi df-ima 3eqtr4i ) AABCZDZCZEKEALFABFMKABG
    HALIABIJ $.
    $( [8-Apr-2007] $)

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z C $.
    $( A composition is a relation.  Exercise 24 of [TakeutiZaring] p. 25. $)
    relco $p |- Rel ( A o. B ) $=
      ( vx vz vy ccom wrel cv wbr wa wex copab relopab df-co releqi mpbir ) ABF
      ZGCHDHZBIREHAIJDKZCELZGSCEMQTCEDABNOP $.
      $( [26-Jan-1997] $)

    $( Alternate definition of a class composition, using only one bound
       variable. $)
    dfco2 $p |- ( A o. B )
                = U_ x e. _V ( ( `' B " { x } ) X. ( A " { x } ) ) $=
      ( vy vz ccom cvv ccnv cv csn cima cxp wrel wcel cop wex vex elimasn bitri
      wa ciun relco reliun relxp a1i mprgbir wb opelco2g mp2an wrex rexv opelxp
      eliun opelcnv anbi12i exbii 3bitrri eqrelriiv ) DEBCFZAGCHZAIZJZKZBVBKZLZ
      UAZBCUBVFMVEMZAGAGVEUCVGVAGNVCVDUDUEUFDIZEIZOZUSNZVHVAOCNZVAVIOBNZTZAPZVJ
      VFNZVHGNVIGNVKVOUGDQZEQZAVHVIBCGGUHUIVPVJVENZAGUJVSAPVOAVJGVEUMVSAUKVSVNA
      VSVHVCNZVIVDNZTVNVHVIVCVDVRULVTVLWAVMVTVAVHOUTNVLUTVAVHAQZVQRVAVHCWBVQUNS
      BVAVIWBVRRUOSUPUQSUR $.
      $( [19-Dec-2008] $)

    $( Generalization of ~ dfco2 , where ` C ` can have any value between
       ` dom A i^i ran B ` and ` _V ` .  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    dfco2a $p |- ( ( dom A i^i ran B ) C_ C -> ( A o. B )
         = U_ x e. C ( ( `' B " { x } ) X. ( A " { x } ) ) ) $=
      ( vy vz vw cvv cv cima ciun wcel wrex wex wa cop vex sylbi 3bitr4g eliun
      cdm crn cin wss ccom ccnv csn cxp dfco2 elimasn opeldm wbr eliniseg ax-mp
      wceq wb brelrn anim12i ancoms adantl exlimivv elxp elin 3imtr4i ssel syl5
      pm4.71rd exbidv rexv df-rex eqrdv syl5eq ) BUAZCUBZUCZDUDZBCUEAHCUFAIZUGZ
      JZBVRJZUHZKZADWAKZABCUIVPEWBWCVPEIZWALZAHMZWEADMZWDWBLWDWCLVPWEANVQDLZWEO
      ZANWFWGVPWEWIAVPWEWHWEVQVOLZVPWHWDFIZGIZPUOZWKVSLZWLVTLZOZOZGNFNVQVMLZVQV
      NLZOZWEWJWQWTFGWPWTWMWOWNWTWOWRWNWSWOVQWLPBLWRBVQWLAQZGQUJVQWLBXAUKRWNWKV
      QCULZWSVQHLWNXBUPXACVQWKHFQZUMUNWKVQCXCXAUQRURUSUTVAFGWDVSVTVBVQVMVNVCVDV
      ODVQVEVFVGVHWEAVIWEADVJSAWDHWATAWDDWATSVKVL $.
      $( [27-Aug-2011] $) $( [21-Dec-2008] $)

    $( Class composition distributes over union.  (The proof was shortened by
       Andrew Salmon, 27-Aug-2011.) $)
    coundi $p |- ( A o. ( B u. C ) ) = ( ( A o. B ) u. ( A o. C ) ) $=
      ( vx vz vy cv wbr wa wex copab cun ccom wo unopab brun anbi1i andir bitri
      df-co exbii 19.43 bitr2i opabbii eqtri uneq12i 3eqtr4ri ) DGZEGZBHZUIFGAH
      ZIZEJZDFKZUHUICHZUKIZEJZDFKZLZUHUIBCLZHZUKIZEJZDFKZABMZACMZLAUTMUSUMUQNZD
      FKVDUMUQDFOVGVCDFVCULUPNZEJVGVBVHEVBUJUONZUKIVHVAVIUKUHUIBCPQUJUOUKRSUAUL
      UPEUBUCUDUEVEUNVFURDFEABTDFEACTUFDFEAUTTUG $.
      $( [27-Aug-2011] $) $( [21-Dec-2008] $)

    $( Class composition distributes over union.  (The proof was shortened by
       Andrew Salmon, 27-Aug-2011.) $)
    coundir $p |- ( ( A u. B ) o. C ) = ( ( A o. C ) u. ( B o. C ) ) $=
      ( vx vy vz cv wbr wa wex copab cun ccom wo unopab brun anbi2i bitri df-co
      andi exbii 19.43 bitr2i opabbii eqtri uneq12i 3eqtr4ri ) DGEGZCHZUHFGZAHZ
      IZEJZDFKZUIUHUJBHZIZEJZDFKZLZUIUHUJABLZHZIZEJZDFKZACMZBCMZLUTCMUSUMUQNZDF
      KVDUMUQDFOVGVCDFVCULUPNZEJVGVBVHEVBUIUKUONZIVHVAVIUIUHUJABPQUIUKUOTRUAULU
      PEUBUCUDUEVEUNVFURDFEACSDFEBCSUFDFEUTCSUG $.
      $( [27-Aug-2011] $) $( [21-Dec-2008] $)

    $( Restricted first member of a class composition.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    cores $p |- ( ran B C_ C -> ( ( A |` C ) o. B ) = ( A o. B ) ) $=
      ( vz vy vx crn wss cv wbr cres wa wex copab ccom wcel wb vex brelrn df-co
      ssel iba brres syl6rbbr syl56 pm5.32d exbidv opabbidv 3eqtr4g ) BGZCHZDIZ
      EIZBJZUMFIZACKZJZLZEMZDFNUNUMUOAJZLZEMZDFNUPBOABOUKUSVBDFUKURVAEUKUNUQUTU
      NUMUJPUKUMCPZUQUTQULUMBDRERSUJCUMUAVCUTUTVCLUQVCUTUBUMUOACFRUCUDUEUFUGUHD
      FEUPBTDFEABTUI $.
      $( [27-Aug-2011] $) $( [12-Oct-2004] $)

    $( Associative law for the restriction of a composition. $)
    resco $p |- ( ( A o. B ) |` C ) = ( A o. ( B |` C ) ) $=
      ( vx vy vz ccom cres relres relco cv wbr wcel wex vex anbi1i 19.41v brres
      wa brco an32 bitr4i exbii 3bitr2i 3bitr4i eqbrriv ) DEABGZCHZABCHZGZUGCIA
      UIJDKZEKZUGLZUKCMZSZUKFKZUILZUPULALZSZFNZUKULUHLUKULUJLUOUKUPBLZURSZFNZUN
      SVBUNSZFNUTUMVCUNFUKULABDOZEOZTPVBUNFQVDUSFVDVAUNSZURSUSVAURUNUAUQVGURUKU
      PBCFORPUBUCUDUKULUGCVFRFUKULAUIVEVFTUEUF $.
      $( [12-Dec-2006] $)

    $( Image of the composition of two classes.  (Contributed by Jason
       Orendorff, 12-Dec-2006.) $)
    imaco $p |- ( ( A o. B ) " C ) = ( A " ( B " C ) ) $=
      ( vx vy vz ccom cima cv wbr wrex wcel wa wex df-rex vex elima brco rexbii
      exbii bitri rexcom4 r19.41v 3bitri anbi1i bitr4i 3bitr4ri eqriv ) DABGZCH
      ZABCHZHZEIZDIZAJZEUKKUMUKLZUOMZENZUNULLUNUJLZUOEUKOEUNAUKDPZQUSFIZUMBJZFC
      KZUOMZENZURUSVBUOMZENZFCKZVFFCKZENVEUSVAUNUIJZFCKVHFUNUICUTQVJVGFCEVAUNAB
      FPUTRSUAVFFECUBVIVDEVBUOFCUCTUDUQVDEUPVCUOFUMBCEPQUETUFUGUH $.
      $( [11-Dec-2006] $)

    $( The range of the composition of two classes. $)
    rnco $p |- ran ( A o. B ) = ran ( A |` ran B ) $=
      ( vy vx vz ccom crn cres cv wbr wex wcel wa vex exbii excom ancom 3bitr4i
      brco elrn 19.41v anbi2i brres bitr4i 3bitri eqriv ) CABFZGZABGZHZGZDIZCIZ
      UGJZDKZEIZUMUJJZEKZUMUHLUMUKLUOULUPBJZUPUMAJZMZEKZDKVADKZEKURUNVBDEULUMAB
      DNCNZSOVADEPVCUQEVCUTUPUILZMZUQUSDKZUTMUTVGMVCVFVGUTQUSUTDUAVEVGUTDUPBENT
      UBRUPUMAUIVDUCUDOUEDUMUGVDTEUMUJVDTRUF $.
      $( [12-Dec-2006] $)
  $}

  $( The range of the composition of two classes. $)
  rnco2 $p |- ran ( A o. B ) = ( A " ran B ) $=
    ( ccom crn cres cima rnco df-ima eqtr4i ) ABCDABDZEDAJFABGAJHI $.
    $( [27-Mar-2008] $)

  $( The domain of a composition.  Exercise 27 of [Enderton] p. 53. $)
  dmco $p |- dom ( A o. B ) = ( `' B " dom A ) $=
    ( ccom cdm ccnv crn cima dfdm4 cnvco rneqi rnco2 imaeq2i eqtr4i 3eqtri ) AB
    CZDOEZFBEZAEZCZFZQADZGZOHPSABIJTQRFZGUBQRKUAUCQAHLMN $.
    $( [4-Feb-2004] $)

  ${
    $d w x y z A $.  $d w y z B $.  $d w y z C $.
    $( Composition with an indexed union. $)
    coiun $p |- ( A o. U_ x e. C B ) = U_ x e. C ( A o. B ) $=
      ( vy vz vw ciun ccom relco wrel cv wcel wbr wa wex wrex cop eliun df-br
      reliun a1i mprgbir rexbii 3bitr4i anbi1i r19.41v bitr4i exbii rexcom4 vex
      opelco bitri eqrelriiv ) EFBADCHZIZADBCIZHZBUOJURKUQKZADADUQUAUSALDMBCJUB
      UCELZGLZUONZVAFLZBNZOZGPZUTVACNZVDOZGPZADQZUTVCRZUPMVKURMZVFVHADQZGPVJVEV
      MGVEVGADQZVDOVMVBVNVDUTVARZUOMVOCMZADQVBVNAVODCSUTVAUOTVGVPADUTVACTUDUEUF
      VGVDADUGUHUIVHAGDUJUHGUTVCBUOEUKZFUKZULVLVKUQMZADQVJAVKDUQSVSVIADGUTVCBCV
      QVRULUDUMUEUN $.
      $( [21-Dec-2008] $)
  $}

  $( A composition is not affected by a double converse of its first
     argument. $)
  cocnvcnv1 $p |- ( `' `' A o. B ) = ( A o. B ) $=
    ( ccnv ccom cvv cres cnvcnv2 coeq1i crn wss wceq ssv cores ax-mp eqtri ) AC
    CZBDAEFZBDZABDZPQBAGHBIZEJRSKTLABEMNO $.
    $( [8-Oct-2007] $)

  $( A composition is not affected by a double converse of its second
     argument. $)
  cocnvcnv2 $p |- ( A o. `' `' B ) = ( A o. B ) $=
    ( ccnv ccom cres cnvcnv2 coeq2i resco wrel wceq relco dfrel3 mpbi 3eqtr2i
    cvv ) ABCCZDABOEZDABDZOEZRPQABFGABOHRISRJABKRLMN $.
    $( [8-Oct-2007] $)

  $( Absorption of a reverse (preimage) restriction of the second member of a
     class composition. $)
  cores2 $p |- ( dom A C_ C -> ( A o. `' ( `' B |` C ) ) = ( A o. B ) ) $=
    ( cdm ccnv cres ccom wceq dfdm4 sseq1i cores sylbi cnvco wrel relres dfrel2
    wss crn mpbi relco coeq1i eqtri 3eqtr4g cnveqd 3eqtr3g ) ADZCQZABEZCFZEZGZE
    ZEZABGZEZEZUKUNUGULUOUGUIAEZGZUHUQGZULUOUGUQRZCQURUSHUFUTCAIJUHUQCKLULUJEZU
    QGURAUJMVAUIUQUINVAUIHUHCOUIPSUAUBABMUCUDUKNUMUKHAUJTUKPSUNNUPUNHABTUNPSUE
    $.
    $( [11-Dec-2006] $)

  ${
    $d x y z A $.
    $( Composition with the empty set.  Theorem 20 of [Suppes] p. 63. $)
    co02 $p |- ( A o. (/) ) = (/) $=
      ( vx vy vz c0 ccom relco rel0 cv cop wcel wbr wa noel df-br mtbir intnanr
      wex nex vex opelco 2false eqrelriiv ) BCAEFZEAEGHBIZCIZJZUDKZUGEKUHUEDIZE
      LZUIUFALZMZDRULDUJUKUJUEUIJZEKUMNUEUIEOPQSDUEUFAEBTCTUAPUGNUBUC $.
      $( [24-Apr-2004] $)

    $( Composition with the empty set. $)
    co01 $p |- ( (/) o. A ) = (/) $=
      ( ccnv ccom cnv0 coeq2i co02 eqtr2i cnvco 3eqtr4i cnveqi wrel wceq dfrel2
      c0 rel0 mpbi relco 3eqtr3ri ) NBZBZNACZBZBZNUASUBNABZSCZSUBUEUDNCNSNUDDEU
      DFGDNAHIJNKTNLONMPUAKUCUALNAQUAMPR $.
      $( [24-Apr-2004] $)

    $( Composition with the identity relation.  Part of Theorem 3.7(i) of
       [Monk1] p. 36. $)
    coi1 $p |- ( Rel A -> ( A o. _I ) = A ) $=
      ( vx vy vz cid ccom wrel wceq relco cv cop wcel wbr wa wex vex ideq eqcom
      opelco bitri anbi1i exbii breq1 ceqsexv 3bitri df-br eqrelriv mpan ) AEFZ
      GAGUIAHAEIBCUIABJZCJZKZUILZUJUKAMZULALUMUJDJZEMZUOUKAMZNZDOUOUJHZUQNZDOUN
      DUJUKAEBPZCPSURUTDUPUSUQUPUJUOHUSUJUODPQUJUORTUAUBUQUNDUJVAUOUJUKAUCUDUEU
      JUKAUFTUGUH $.
      $( [22-Apr-2004] $)

    $( Composition with the identity relation.  Part of Theorem 3.7(i) of
       [Monk1] p. 36. $)
    coi2 $p |- ( Rel A -> ( _I o. A ) = A ) $=
      ( wrel ccom ccnv cnvco wceq relcnv ax-mp cnveqi eqtr3i dfrel2 coeq2 coeq1
      cid coi1 cnvi sylan9eq mpan2 sylbi syl5reqr biimpi eqtrd ) ABZNACZADZDZAU
      CUFNDZUFCZUDUENCZDUHUFUENEUIUEUEBUIUEFAGUEOHIJUCUFAFZUHUDFZAKZUJUGNFZUKPU
      JUMUHUGACUDUFAUGLUGNAMQRSTUCUJULUAUB $.
      $( [22-Apr-2004] $)
  $}

  $( Composition with a restricted identity relation.  (Contributed by FL,
     19-Jun-2011.) $)
  coires1 $p |- ( Rel A
     -> ( A o. ( _I |` B ) ) = ( A |` B ) ) $=
    ( wrel cid cres ccom resco wceq coi1 reseq1 syl syl5eqr ) ACZADBEFADFZBEZAB
    EZADBGMNAHOPHAINABJKL $.
    $( [19-Jun-2011] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.
    $( Associative law for class composition.  Theorem 27 of [Suppes] p. 64.
       Also Exercise 21 of [Enderton] p. 53.  Interestingly, this law holds for
       any classes whatsoever, not just functions or even relations. $)
    coass $p |- ( ( A o. B ) o. C ) = ( A o. ( B o. C ) ) $=
      ( vx vy vz vw ccom relco cv wbr wa wex cop df-br vex opelco exbii 3bitr4i
      wcel excom anass 2exbii bitr4i anbi2i 19.42v anbi1i 19.41v eqrelriiv
      bitri ) DEABHZCHZABCHZHZUKCIAUMIDJZFJZCKZUPGJZBKZUREJZAKZLZLZGMZFMZUQUSLZ
      VALZFMZGMZUOUTNZULTZVJUNTZVEVCFMGMVIVCFGUAVGVCGFUQUSVAUBUCUDUQUPUTUKKZLZF
      MUQVBGMZLZFMVKVEVNVPFVMVOUQVMUPUTNUKTVOUPUTUKOGUPUTABFPEPZQUJUERFUOUTUKCD
      PZVQQVDVPFUQVBGUFRSUOURUMKZVALZGMVFFMZVALZGMVLVIVTWBGVSWAVAVSUOURNUMTWAUO
      URUMOFUOURBCVRGPQUJUGRGUOUTAUMVRVQQVHWBGVFVAFUHRSSUI $.
      $( [27-Jan-1997] $)
  $}

  $( A relation is transitive iff its converse is transitive.  (Contributed by
     FL, 19-Sep-2011.) $)
  relcnvtr $p |- ( Rel R ->
     ( ( R o. R ) C_ R <-> ( `' R o. `' R ) C_ `' R ) ) $=
    ( wrel ccom wss ccnv cnvco cnvss syl5eqssr wceq wi sseq1 dfrel2 coeq1 coeq2
    eqtrd id sseq12d biimpd sylbi com12 syl6bi mpsyl impbid2 ) ABZAACZADZAEZUGC
    ZUGDZUFUHUEEUGAAFUEAGHUIUDUFUHEZUGEZUKCZIZUIUJUKDZUDUFJZUGUGFUHUGGUMUNULUKD
    ZUOUJULUKKUDUPUFUDUKAIZUPUFJALUQUPUFUQULUEUKAUQULAUKCUEUKAUKMUKAANOUQPQRSTU
    AUBTUC $.
    $( [19-Sep-2011] $)

  ${
    $d x y A $.
    $( A relation is included in the cross product of its domain and range.
       Exercise 4.12(t) of [Mendelson] p. 235. $)
    relssdmrn $p |- ( Rel A -> A C_ ( dom A X. ran A ) ) $=
      ( vx vy wrel cdm crn cxp id cv cop wi wex 19.8a wa vex opelxp eldm2 elrn2
      wcel anbi12i bitri sylanbrc a1i relssdv ) ADZBCAAEZAFZGZUEHBIZCIZJZASZUKU
      HSZKUEULULCLZULBLZUMULCMULBMUMUIUFSZUJUGSZNUNUONUIUJUFUGCOZPUPUNUQUOCUIAB
      OQBUJAURRTUAUBUCUD $.
      $( [3-Aug-1994] $)
  $}

  $( Two ways to describe the structure of a two-place operation. $)
  relrelss $p |- ( ( Rel A /\ Rel dom A ) <-> A C_ ( ( _V X. _V ) X. _V ) ) $=
    ( wrel cdm wa cvv cxp wss df-rel anbi2i crn relssdmrn xpss12 mpan2 sylan9ss
    ssv xpss sstr sylibr dmss c0 wne wceq vn0 dmxp ax-mp syl6sseq impbii bitri
    jca ) ABZACZBZDUJUKEEFZGZDZAUMEFZGZULUNUJUKHIUOUQUJUNAUKAJZFZUPAKUNUREGUSUP
    GUROUKUMURELMNUQUJUNUQAUMGZUJUQUPUMGUTUMEPAUPUMQMAHRUQUKUPCZUMAUPSETUAVAUMU
    BUCUMEUDUEUFUIUGUH $.
    $( [17-Dec-2008] $)

  ${
    $d x y A $.  $d x y R $.
    $( The membership relation for a relation is inherited by class union. $)
    unielrel $p |- ( ( Rel R /\ A e. R ) -> U. A e. U. R ) $=
      ( vx vy wrel wcel wa cv cop wceq wex cuni elrel simpr uniopel eleq1 unieq
      wi a1i eleq1d 3imtr4d exlimivv sylc ) BEZABFZGACHZDHZIZJZDKCKUEALZBLZFZCD
      ABMUDUENUIUEULRCDUIUHBFZUHLZUKFZUEULUMUORUIUFUGBOSAUHBPUIUJUNUKAUHQTUAUBU
      C $.
      $( [17-Sep-2006] $)
  $}

  $( The double union of a relation is its field. $)
  relfld $p |- ( Rel R -> U. U. R = ( dom R u. ran R ) ) $=
    ( wrel cuni cdm crn cun cxp wss relssdmrn 3syl unixpss syl6ss dmrnssfld a1i
    uniss eqssd ) ABZACZCZADZAEZFZQSTUAGZCZCZUBQAUCHRUDHSUEHAIAUCORUDOJTUAKLUBS
    HQAMNP $.
    $( [17-Sep-2006] $)

  $( Restriction of a relation to its field.  (Contributed by FL,
     15-Apr-2012.) $)
  relresfld $p |- ( Rel R -> ( R |` U. U. R ) = R ) $=
    ( wrel cuni cres wceq cdm crn cun wi relfld reseq2 resundi eqtr resss resdm
    wa wss ssequn2 uneq1 eqeq2d ex com3r sylbi mpsyl syl5com mpan2 3syl pm2.43i
    syl6bi ) ABZAACCZDZAEZUJUKAFZAGZHZEULAUPDZEZUJUMIZAJUKUPAKURUQAUNDZAUODZHZE
    ZUSAUNUOLURVCPULVBEZUJUMULUQVBMVAAQZUJUTAEZVDUMIZAUONAOVEAVAHZAEZVFVGIVAARV
    FVDVIUMVFVDULVHEZVIUMIVFVBVHULUTAVASTVJVIUMULVHAMUAUIUBUCUDUEUFUGUH $.
    $( [15-Apr-2012] $)

  $( Composition with the identity relation restricted to a relation's field.
     (Contributed by FL, 2-May-2011.) $)
  relcoi2 $p |- ( Rel R -> ( ( _I |` U. U. R ) o. R ) = R ) $=
    ( wrel cid cuni cres ccom crn wss wceq cdm cun dmrnssfld simpr sylbir ax-mp
    wa unss a1i cores syl coi2 eqtrd ) ABZCADDZEAFZCAFZAUCAGZUDHZUEUFIUHUCAJZUG
    KUDHZUHALUJUIUDHZUHPUHUIUGUDQUKUHMNORCAUDSTAUAUB $.
    $( [2-May-2011] $)

  $( Composition with the identity relation restricted to a relation's field.
     (Contributed by FL, 8-May-2011.) $)
  relcoi1 $p |- ( Rel R -> ( R o. ( _I |` U. U. R ) ) = R ) $=
    ( wrel cid cuni cres ccom cun wceq wi resco reseq1 wa eqtr eqeq1 syl5ibr ex
    syl com3l eqcoms mpcom cdm crn relfld resundi coeq2 coundi coi1 resdm uneq1
    uneq2 wss resss ssequn2 mpbi syl6reqr mpsyl syl5com mpi syl5eq ax-mp reseq2
    coeq2d eqeq1d eqtrd ) ABZACADDZEZFZACFZAVFAUAZAUBZGZHZVEVHVIHZAUCVEVNVMACVL
    EZFZVIHZVOCVJEZCVKEZGZHZVEVQIZCVJVKUDWAVPAVTFZHZWBVOVTAUEVEVQWDWCVIHVEWCAVR
    FZAVSFZGZVIAVRVSUFVEVIVJEZWEHZWGVIHZACVJJVIAHZVEWIWJIZAUGZWKWHAVJEZHZVEWLVI
    AVJKWNAHZVEWOWLIAUHWOWPVEWLWOWPVEWLIZWOWPLWHAHZWQWHWNAMWIWRVEWJWRVEWJIZIWEW
    HWEWHHZWRWSWTWRLWEAHZWSWEWHAMVIVKEZWFHZXAWGAWFGZHZWSACVKJWEAWFUIVEXCXEWJWKV
    EXCXEWJIZIZWMWKXBAVKEZHZVEXGIVIAVKKXCXIVEXFXIVEXFIZIWFXBWFXBHZXIXJXKXILWFXH
    HZXJWFXBXHMXLXDAXHGZHZXJWFXHAUJXEXNVEWJXEXNWSXEXNLWGXMHZWSWGXDXMMVEWJXOXMVI
    HVEVIAXMWMXHAUKXMAHAVKULXHAUMUNUOWGXMVINOQPRQQPSRQTRUPQPSRQPRTUQTURUSVPWCVI
    NOQUTVMVHVPVIVMVGVOAVFVLCVAVBVCOTWMVD $.
    $( [8-May-2011] $)

  $( The double union of the converse of a class is its field. $)
  unidmrn $p |- U. U. `' A = ( dom A u. ran A ) $=
    ( ccnv crn cdm cuni uncom dfdm4 df-rn uneq12i wrel wceq relcnv relfld ax-mp
    cun 3eqtr4ri ) ABZCZQDZOSROZADZACZOQEEZRSFUARUBSAGAHIQJUCTKALQMNP $.
    $( [4-Jun-2008] $)

  $( if ` R ` is a relation, its double union equals the double union of its
     converse.  (Contributed by FL, 5-Jan-2009.) $)
  relcnvfld $p |- ( Rel R -> U. U. R = U. U. `' R ) $=
    ( wrel cuni cdm crn cun ccnv relfld unidmrn syl6eqr ) ABACCADAEFAGCCAHAIJ
    $.
    $( [5-Jan-2009] $)

  $( Alternate definition of domain ~ df-dm that doesn't require dummy
     variables. $)
  dfdm2 $p |- dom A = U. U. ( `' A o. A ) $=
    ( ccnv ccom cuni cdm crn cun cnvco cocnvcnv2 eqtri unieqi eqtr3i wceq df-rn
    unidmrn eqcomi dmcoeq ax-mp rncoeq dfdm4 eqtr4i uneq12i unidm 3eqtrri ) ABZ
    ACZDZDZUFEZUFFZGZAEZULGULUFBZDZDUHUKUNUGUMUFUMUEUEBCUFUEAHUEAIJKKUFOLUIULUJ
    ULUEEZAFZMZUIULMUPUOANPZUEAQRUJUEFZULUQUJUSMURUEASRATUAUBULUCUD $.
    $( [2-Aug-2010] $)

  $( The double class union of a non-empty cross product is the union of it
     members. $)
  unixp $p |- ( ( A X. B ) =/= (/) -> U. U. ( A X. B ) = ( A u. B ) ) $=
    ( cxp c0 wne cuni cdm crn cun wrel wceq relxp relfld ax-mp xpeq2 xp0 syl6eq
    necon3i xpeq1 xp0r dmxp rnxp uneq12 syl2an syl2anc syl5eq ) ABCZDEZUGFFZUGG
    ZUGHZIZABIZUGJUIULKABLUGMNUHBDEZADEZULUMKZBDUGDBDKUGADCDBDAOAPQRADUGDADKUGD
    BCDADBSBTQRUNUJAKUKBKUPUOABUAABUBUJAUKBUCUDUEUF $.
    $( [17-Sep-2006] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( A cross product is empty iff its union is empty. $)
    unixp0 $p |- ( ( A X. B ) = (/) <-> U. ( A X. B ) = (/) ) $=
      ( vz vx vy cxp c0 wceq cuni unieq uni0 syl6eq wn cv wcel wex neq0 cop wss
      sylbi wa elxp3 csn snssi opex unisn uniss syl5eqssr opnz sseq2 biimpd ss0
      syl6com mtoi 3syl adantl exlimivv exlimiv con4i impbii ) ABFZGHZVAIZGHZVB
      VCGIGVAGJKLVBVDVBMCNZVAOZCPVDMZCVAQVFVGCVFDNZENZRZVEHZVJVAOZUAZEPDPVGDEVE
      ABUBVMVGDEVLVGVKVLVJUCZVASZVJVCSZVGVJVAUDVOVJVNIVCVJVHVIUEUFVNVAUGUHVPVDV
      JGHZVHVIUIVDVPVJGSZVQVDVPVRVCGVJUJUKVJULUMUNUOUPUQTURTUSUT $.
      $( [20-Sep-2006] $)
  $}

  $( The converse of a set is a set.  Corollary 6.8(1) of [TakeutiZaring]
     p. 26. $)
  cnvexg $p |- ( A e. V -> `' A e. _V ) $=
    ( wcel ccnv cdm crn cxp wss cvv wrel relcnv relssdmrn ax-mp df-rn syl5eqelr
    rnexg dfdm4 dmexg xpexg syl2anc ssexg sylancr ) ABCZADZUDEZUDFZGZHZUGICZUDI
    CUDJUHAKUDLMUCUEICUFICUIUCUEAFIANABPOUCUFAEIAQABROUEUFIISTUDUGIUAUB $.
    $( [17-Mar-1998] $)

  ${
    cnvex.1 $e |- A e. _V $.
    $( The converse of a set is a set.  Corollary 6.8(1) of [TakeutiZaring]
       p. 26. $)
    cnvex $p |- `' A e. _V $=
      ( cvv wcel ccnv cnvexg ax-mp ) ACDAECDBACFG $.
      $( [19-Dec-2003] $)
  $}

  $( A relation is a set iff its converse is a set.  (Contributed by FL,
     3-Mar-2007.) $)
  relcnvexb $p |- ( Rel R -> ( R e. _V <-> `' R e. _V ) ) $=
    ( wrel cvv wcel ccnv cnvexg wceq wi dfrel2 eleq1 syl5ib sylbi impbid2 ) ABZ
    ACDZAEZCDZACFNPEZAGZQOHAIQRCDSOPCFRACJKLM $.
    $( [5-Mar-2007] $)

  ${
    $d A a b x $.  $d B a b $.
    $( The converse of an intersection is the intersection of the converse.
       (Contributed by FL, 15-Oct-2012.) $)
    cnviin $p |- ( A =/= (/) -> `' |^|_ x e. A B = |^|_ x e. A `' B ) $=
      ( va vb ciin ccnv wrel relcnv cvv wss wral cv wcel df-rel cop eliin ax-mp
      wb opex c0 wne wceq cxp wrex r19.2z expcom mpbi a1i mprg iinss syl sylibr
      wi vex opelcnv ralbii bitri 3bitr4i eqrelriv sylancr ) BUAUBZABCFZGZHABCG
      ZFZHZVDVFUCVCIVBVFJJUDZKZVGVBVEVHKZABUEZVIVJVBVKUNABVBVJABLVKVJABUFUGVJAM
      BNVEHVJCIVEOUHUIUJABVEVHUKULVFOUMDEVDVFEMZDMZPZVCNZVNCNZABLZVMVLPZVDNVRVF
      NZVNJNVOVQSVLVMTAVNBCJQRVMVLVCDUOZEUOZUPVSVRVENZABLZVQVRJNVSWCSVMVLTAVRBV
      EJQRWBVPABVMVLCVTWAUPUQURUSUTVA $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.
    $( The converse of a partial order relation is a partial order relation. $)
    cnvpo $p |- ( R Po A <-> `' R Po A ) $=
      ( vx vy vz cv wbr wn wa wi wral wpo r19.26 ralbii c0 wceq rzal vex ralcom
      brcnv ccnv ralidm wb 2thd r19.3rzv ralbidv pm2.61ine bitr2i anbi1i 3bitri
      wne bitr4i id breq12d syl5bb notbid cbvralv anbi12i ancom imbi12i 3bitr4i
      bitri df-po ) CFZVDBGZHZVDDFZBGZVGEFZBGZIZVDVIBGZJZIEAKZDAKCAKZVIVIBUAZGZ
      HZVIVGVPGZVGVDVPGZIZVIVDVPGZJZIZCAKZDAKEAKZABLAVPLVNCAKZDAKWEEAKZDAKVOWFW
      GWHDAWGVFCAKZVMEAKZIZCAKZWDEAKZCAKWHWGWICAKZWJCAKZIZWLWGVFEAKZWJIZCAKWQCA
      KZWOIWPVNWRCAVFVMEAMNWQWJCAMWSWNWOWNWIWSVFCAUBWIWSUCAOAOPWIWSVFCAQWQCAQUD
      AOUKVFWQCAVFEAUEUFUGUHUIUJWIWJCAMULWKWMCAWMVREAKZWCEAKZIWKVRWCEAMWTWIXAWJ
      VRVFECAVIVDPZVQVEVQVIVIBGXBVEVIVIBERZXCTXBVIVDVIVDBXBUMZXDUNUOUPUQWCVMEAW
      AVKWBVLWAVJVHIVKVSVJVTVHVIVGBXCDRZTVGVDBXECRZTURVJVHUSVBVIVDBXCXFTUTNURUH
      NWDCEAASUJNVNCDAASWEEDAASVACDEABVCEDCAVPVCVA $.
      $( [15-Jun-2005] $)

    $( The converse of a strict order relation is a strict order relation. $)
    cnvso $p |- ( R Or A <-> `' R Or A ) $=
      ( vx vy wpo cv wbr wceq w3o wral wa ccnv wor cnvpo ralcom vex brcnv eqcom
      3orbi123i df-so 2ralbii bitr4i anbi12i 3bitr4i ) ABEZCFZDFZBGZUFUGHZUGUFB
      GZIZDAJCAJZKABLZEZUGUFUMGZUGUFHZUFUGUMGZIZCAJDAJZKABMAUMMUEUNULUSABNULUKC
      AJDAJUSUKCDAAOURUKDCAAUOUHUPUIUQUJUGUFBDPZCPZQUGUFRUFUGBVAUTQSUAUBUCCDABT
      DCAUMTUD $.
      $( [15-Jun-2005] $)
  $}

  $( The composition of two sets is a set. $)
  coexg $p |- ( ( A e. V /\ B e. W ) -> ( A o. B ) e. _V ) $=
    ( wcel wa ccom cdm crn cxp wss cvv wrel relco relssdmrn dmcoss rncoss mp2an
    xpss12 sstr2 mpi syl ax-mp dmexg rnexg xpexg syl2an ancoms ssexg sylancr )
    ACEZBDEZFABGZBHZAIZJZKZUPLEZUMLEUMMZUQABNUSUMUMHZUMIZJZKZUQUMOVCVBUPKZUQUTU
    NKVAUOKVDABPABQUTUNVAUOSRUMVBUPTUAUBUCULUKURULUNLEUOLEURUKBDUDACUEUNUOLLUFU
    GUHUMUPLUIUJ $.
    $( [19-Mar-1998] $)

  ${
    coex.1 $e |- A e. _V $.
    coex.2 $e |- B e. _V $.
    $( The composition of two sets is a set. $)
    coex $p |- ( A o. B ) e. _V $=
      ( cvv wcel ccom coexg mp2an ) AEFBEFABGEFCDABEEHI $.
      $( [15-Dec-2003] $)
  $}

  ${
    $d x y z A $.
    $( Alternate definition of a function. $)
    dffun2 $p |- ( Fun A <-> ( Rel A /\
                 A. x A. y A. z ( ( x A y /\ x A z ) -> y = z ) ) ) $=
      ( wfun wrel cid wss wa cv wbr wal wex copab 3bitri vex albii alcom bitri
      wi ccnv ccom df-fun df-id sseq2i df-co sseq1i ssopab2b brcnv anbi1i exbii
      wceq imbi1i 19.23v bitr4i anbi2i ) DEDFZDDUAZUBZGHZIUQAJZBJZDKZVACJZDKZIZ
      VBVDULZTZCLZBLALZIDUCUTVJUQUTVBVAURKZVEIZAMZVGTZCLZBLZVIALZBLVJUTUSVGBCNZ
      HVMBCNZVRHVPGVRUSBCUDUEUSVSVRBCADURUFUGVMVGBCUHOVOVQBVOVHALZCLVQVNVTCVNVF
      AMZVGTVTVMWAVGVLVFAVKVCVEVBVADBPAPUIUJUKUMVFVGAUNUOQVHCARSQVIBAROUPS $.
      $( [23-Apr-2004] $) $( [29-Dec-1996] $)

    $( Alternate definition of function. $)
    dffun3 $p |- ( Fun A <-> ( Rel A /\
                 A. x E. z A. y ( x A y -> y = z ) ) ) $=
      ( wfun wrel cv wbr wa wceq wal wex dffun2 wmo breq2 mo4 ax-17 mo2 bitr3i
      wi albii anbi2i bitri ) DEDFZAGZBGZDHZUECGZDHZIUFUHJZTCKBKZAKZIUDUGUJTBKC
      LZAKZIABCDMULUNUDUKUMAUKUGBNUMUGUIBCUFUHUEDOPUGBCUGCQRSUAUBUC $.
      $( [23-Apr-2004] $) $( [29-Dec-1996] $)

    $( Alternate definition of a function.  Definition 6.4(4) of
       [TakeutiZaring] p. 24. $)
    dffun4 $p |- ( Fun A <-> ( Rel A /\
                 A. x A. y A. z ( ( <. x , y >. e. A /\ <. x , z >. e. A )
                 -> y = z ) ) ) $=
      ( wfun wrel cv wbr wa wceq wal cop wcel dffun2 df-br anbi12i imbi1i albii
      wi 2albii anbi2i bitri ) DEDFZAGZBGZDHZUDCGZDHZIZUEUGJZSZCKZBKAKZIUCUDUEL
      DMZUDUGLDMZIZUJSZCKZBKAKZIABCDNUMUSUCULURABUKUQCUIUPUJUFUNUHUOUDUEDOUDUGD
      OPQRTUAUB $.
      $( [29-Dec-1996] $)

    $( Alternate definition of function. $)
    dffun5 $p |- ( Fun A <-> ( Rel A /\
                 A. x E. z A. y ( <. x , y >. e. A -> y = z ) ) ) $=
      ( wfun wrel cv wbr wceq wi wal wex wa cop dffun3 df-br imbi1i albii exbii
      wcel anbi2i bitri ) DEDFZAGZBGZDHZUECGIZJZBKZCLZAKZMUCUDUENDTZUGJZBKZCLZA
      KZMABCDOUKUPUCUJUOAUIUNCUHUMBUFULUGUDUEDPQRSRUAUB $.
      $( [29-Dec-1996] $)
  $}

  ${
    $d x y z w v u $.  $d z A w v u $.
    dffunmof.1 $e |- ( z e. A -> A. x z e. A ) $.
    dffunmof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Definition of function, using bound-variable hypotheses instead of
       distinct variable conditions. $)
    dffun6f $p |- ( Fun A <-> ( Rel A /\ A. x E* y x A y ) ) $=
      ( vw vv vu wfun cv wbr wceq wal wa wmo wcel ax-17 hbbr albii wi wex breq2
      wrel dffun3 cbvmo mo2 hbmo breq1 mobid cbval 3bitr3ri anbi2i bitr4i ) DJD
      UDZGKZHKZDLZUQIKMUAHNIUBZGNZOUOAKZBKZDLZBPZANZOGHIDUEVEUTUOURHPZGNUPVBDLZ
      BPZGNUTVEVFVHGURVGHBBCUPUQDCKZUPQZBRFVIUQQBRSVGHRUQVBUPDUCUFTVFUSGURHIURI
      RUGTVHVDGAVGABACUPVBDVJAREVIVBQARSUHVDGRUPVAMZVGVCBVKBRUPVAVBDUIUJUKULUMU
      N $.
      $( [15-Aug-2004] $) $( [9-Mar-1995] $)
  $}

  ${
    $d x y z A $.
    $( Alternate definition of a function using "at most one" notation. $)
    dffun6 $p |- ( Fun A <-> ( Rel A /\ A. x E* y x A y ) ) $=
      ( vz cv wcel ax-17 dffun6f ) ABDCDECFZAGIBGH $.
      $( [15-Aug-2004] $) $( [9-Mar-1995] $)

    $( A function has at most one value for each argument. $)
    funmo $p |- ( Fun A -> E* y x A y ) $=
      ( wfun cv wbr wmo wrel wal dffun6 simprbi 19.21bi ) CDZAEBECFBGZAMCHNAIAB
      CJKL $.
      $( [15-Aug-2004] $) $( [24-May-1998] $)
  $}

  $( A function is a relation. $)
  funrel $p |- ( Fun A -> Rel A ) $=
    ( wfun wrel ccnv ccom cid wss df-fun simplbi ) ABACAADEFGAHI $.
    $( [1-Aug-1994] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( Subclass theorem for function predicate.  (The proof was shortened by
       Andrew Salmon, 17-Sep-2011.) $)
    funss $p |- ( A C_ B -> ( Fun B -> Fun A ) ) $=
      ( vx vy wss wrel cv wbr wmo wa wfun relss wi id ssbrd alrimiv immo dffun6
      wal syl alimdv anim12d 3imtr4g ) ABEZBFZCGZDGZBHZDIZCSZJAFZUFUGAHZDIZCSZJ
      BKAKUDUEUKUJUNABLUDUIUMCUDULUHMZDSUIUMMUDUODUDABUFUGUDNOPULUHDQTUAUBCDBRC
      DARUC $.
      $( [18-Sep-2011] $) $( [16-Aug-1994] $)
  $}

  $( Equality theorem for function predicate. $)
  funeq $p |- ( A = B -> ( Fun A <-> Fun B ) ) $=
    ( wss wa wfun wi wceq wb funss anim12i ancoms eqss dfbi2 3imtr4i ) ABCZBACZ
    DAEZBEZFZRQFZDZABGQRHPOUAPSOTBAIABIJKABLQRMN $.
    $( [16-Aug-1994] $)

  ${
    funeqi.1 $e |- A = B $.
    $( Equality inference for the function predicate.  (Contributed by Jonathan
       Ben-Naim, 3-Jun-2011.) $)
    funeqi $p |- ( Fun A <-> Fun B ) $=
      ( wceq wfun wb funeq ax-mp ) ABDAEBEFCABGH $.
      $( [3-Jun-2011] $)
  $}

  ${
    funeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for the function predicate. $)
    funeqd $p |- ( ph -> ( Fun A <-> Fun B ) ) $=
      ( wceq wfun wb funeq syl ) ABCEBFCFGDBCHI $.
      $( [23-Feb-2013] $)
  $}

  ${
    $d y F $.  $d x y $.
    hbfun.1 $e |- ( y e. F -> A. x y e. F ) $.
    $( Bound-variable hypothesis builder for a function. $)
    hbfun $p |- ( Fun F -> A. x Fun F ) $=
      ( wfun wrel ccnv ccom cid wa df-fun hbrel hbcnv hbco wcel ax-17 hbss hban
      wss cv hbxfrbi ) CECFZCCGZHZISZJACKUBUEAABCDLABUDIABCUCDABCDMNBTIOAPQRUA
      $.
      $( [30-Jan-2004] $)
  $}

  ${
    $d x y z F $.
    $( There is exactly one value of a function.  (The proof was shortened by
       Andrew Salmon, 17-Sep-2011.) $)
    funeu $p |- ( ( Fun F /\ x F y ) -> E! y x F y ) $=
      ( wfun cv wbr weu wex 19.8a wmo wi funmo df-mo sylib syl5 imp ) CDZAEBECF
      ZRBGZRRBHZQSRBIQRBJTSKABCLRBMNOP $.
      $( [17-Aug-2011] $) $( [22-Apr-2004] $)

    $( There is exactly one value of a function. $)
    funeu2 $p |- ( ( Fun F /\ <. x , y >. e. F ) -> E! y <. x , y >. e. F ) $=
      ( wfun cv wbr wa weu cop wcel funeu df-br anbi2i eubii 3imtr3i ) CDZAEZBE
      ZCFZGSBHPQRICJZGTBHABCKSTPQRCLZMSTBUANO $.
      $( [3-Aug-1994] $)
  $}

  ${
    $d x y A $.
    $( Alternate definition of a function.  One possibility for the definition
       of a function in [Enderton] p. 42.  (Enderton's definition is ambiguous
       because "there is only one" could mean either "there is at most one" or
       "there is exactly one."  However, ~ dffun8 shows that it doesn't matter
       which meaning we pick.) $)
    dffun7 $p |- ( Fun A <-> ( Rel A /\ A. x e. dom A E* y x A y ) ) $=
      ( wfun wrel cv wbr wmo wal wa cdm wral dffun6 wcel wi wex vex eldm bitr4i
      moabs imbi1i albii df-ral anbi2i bitri ) CDCEZAFZBFCGZBHZAIZJUFUIACKZLZJA
      BCMUJULUFUJUGUKNZUIOZAIULUIUNAUIUHBPZUIOUNUHBTUMUOUIBUGCAQRUASUBUIAUKUCSU
      DUE $.
      $( [4-Nov-2002] $)

    $( Alternate definition of a function.  One possibility for the definition
       of a function in [Enderton] p. 42.  Compare ~ dffun7 .  (The proof was
       shortened by Andrew Salmon, 17-Sep-2011.) $)
    dffun8 $p |- ( Fun A <-> ( Rel A /\ A. x e. dom A E! y x A y ) ) $=
      ( wfun wrel cv wbr wmo cdm wral wa weu dffun7 wex wi wcel df-mo vex eldm
      wb pm5.5 sylbi syl5bb ralbiia anbi2i bitri ) CDCEZAFZBFCGZBHZACIZJZKUGUIB
      LZAUKJZKABCMULUNUGUJUMAUKUJUIBNZUMOZUHUKPZUMUIBQUQUOUPUMTBUHCARSUOUMUAUBU
      CUDUEUF $.
      $( [18-Sep-2011] $) $( [4-Nov-2002] $)

    $( Alternate definition of a function. $)
    dffun9 $p |- ( Fun A <->
                 ( Rel A /\ A. x e. dom A E* y ( y e. ran A /\ x A y ) ) ) $=
      ( wfun wrel cv wbr wmo cdm wral crn wcel dffun7 vex brelrn pm4.71ri mobii
      wa ralbii anbi2i bitri ) CDCEZAFZBFZCGZBHZACIZJZRUBUDCKLZUERZBHZAUGJZRABC
      MUHULUBUFUKAUGUEUJBUEUIUCUDCANBNOPQSTUA $.
      $( [28-Mar-2007] $)
  $}

  $( An equivalence for the function predicate. $)
  funfn $p |- ( Fun A <-> A Fn dom A ) $=
    ( wfun cdm wceq wa wfn eqid biantru df-fn bitr4i ) ABZKACZLDZEALFMKLGHALIJ
    $.
    $( [13-Aug-2004] $)

  $( The identity relation is a function.  Part of Theorem 10.4 of [Quine]
     p. 65. $)
  funi $p |- Fun _I $=
    ( cid wfun wrel ccnv ccom wss df-fun reli wceq relcnv coi2 ax-mp cnvi eqtri
    eqimssi mpbir2an ) ABACAADZEZAFAGHRARQAQCRQIAJQKLMNOP $.
    $( [30-Apr-1998] $)

  $( The universe is not a function.  (Contributed by Raph Levien,
     27-Jan-2004.) $)
  nfunv $p |- -. Fun _V $=
    ( cvv wfun wrel cxp wcel 0nelxp 0ex wss df-rel biimpi sseld mpi mto funrel
    c0 ) ABACZPOAADZEZAAFPOAERGPAQOPAQHAIJKLMANM $.
    $( [27-Jan-2004] $)

  ${
    $d t u v w x y z A $.  $d t u v w x y z B $.
    $( A Kuratowski ordered pair is a function only if its components are
       equal. $)
    funopg $p |- ( ( B e. V /\ Fun <. A , B >. ) -> A = B ) $=
      ( vu vt vx vy vz vw vv wcel cop wfun wceq cvv wi cv imbi12d cpr vex opeq1
      funeqd eqeq1 opeq2 eqeq2 csn wa wex wrel funrel relop sylib zfpair2 dfsn2
      opth preq2i df-op 3eqtr4ri preq1i eqeq2i bitr3i dffun4 simprbi opex prid1
      wal eleq2 mpbiri jca w3a opeq12 3adant3 eleq1d 3adant2 anbi12d wb 3adant1
      prid2 eqeq12 cla43gv mp3an syl2im syl5bi preq2 eqeq2d eqtr3 expcom syl6bi
      syl5req com13 imp sylcom exlimdvv mpd vtocl2g wn c0 opprc1b df-rel 0nelxp
      cxp wss ssel mtoi sylbi syl con2i pm2.21d adantr pm2.61ian ) BCKZABLZMZAB
      NZAOKZXKXMXNPZDQZEQZLZMZXQXRNZPAXRLZMZAXRNZPXPDEABOCXQANZXTYCYAYDYEXSYBXQ
      AXRUAUBXQAXRUCRXRBNZYCXMYDXNYFYBXLXRBAUDUBXRBAUERXTXQFQZUFZNZXRYGGQZSZNZU
      GZGUHFUHZYAXTXSUIZYNXSUJFGXQXRDTZETZUKULXTYMYAFGXTYMYGYJNZYAYMXSYGYGLZYGY
      JLZSZNZXTYRYMXSYHYKLZNUUBXQXRYHYKYPYQFGUMUOUUCUUAXSYSYHYKSZSYHUFZUUDSUUAU
      UCYSUUEUUDYHYHSYHYGYGSZSUUEYSYHUUFYHYGUNZUPYHUNYGYGUQURUSYTUUDYSYGYJUQUPY
      HYKUQURUTVAXTHQZIQZLZXSKZUUHJQZLZXSKZUGZUUIUULNZPZJVFIVFHVFZUUBYSXSKZYTXS
      KZUGZYRXTYOUURHIJXSVBVCUUBUUSUUTUUBUUSYSUUAKYSYTYGYGVDVEXSUUAYSVGVHUUBUUT
      YTUUAKYSYTYGYJVDVRXSUUAYTVGVHVIYGOKZUVBYJOKUURUVAYRPZPFTZUVDGTUUQUVCHIJYG
      YGYJOOOUUHYGNZUUIYGNZUULYJNZVJZUUOUVAUUPYRUVHUUKUUSUUNUUTUVHUUJYSXSUVEUVF
      UUJYSNUVGUUHUUIYGYGVKVLVMUVHUUMYTXSUVEUVGUUMYTNUVFUUHUULYGYJVKVNVMVOUVFUV
      GUUPYRVPUVEUUIYGUULYJVSVQRVTWAWBWCYIYLYRYAPYRYLYIYAYRYLXRYHNZYIYAPYRYKYHX
      RYRYHUUFYKUUGYGYJYGWDWIWEYIUVIYAXQXRYHWFWGWHWJWKWLWMWNWOXOWPZXPXKUVJXMXNU
      VJWQXLKZXMWPABWRXMUVKXMXLUIZUVKWPZXLUJUVLXLOOXAZXBZUVMXLWSUVOUVKWQUVNKOOW
      TXLUVNWQXCXDXEXFXGXEXHXIXJWK $.
      $( [5-Jun-2008] $)
  $}


  ${
    $d x y z $.  $d z ph $.
    $( A class of ordered pairs is a function when there is at most one second
       member for each pair. $)
    funopab $p |- ( Fun { <. x , y >. | ph } <-> A. x E* y ph ) $=
      ( copab wfun wbr wmo wal wrel hbopab1 hbopab2 dffun6f relopab mpbiran cop
      vz cv wcel df-br bitri opabid mobii albii ) ABCDZEZBQZCQZUDFZCGZBHZACGZBH
      UEUDIUJBCPUDABCPJABCPKLABCMNUIUKBUHACUHUFUGOUDRAUFUGUDSABCUATUBUCT $.
      $( [16-May-1995] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( A class of ordered pairs of values is a function. $)
    funopabeq $p |- Fun { <. x , y >. | y = A } $=
      ( cv wceq copab wfun wmo funopab moeq mpgbir ) BDCEZABFGLBHALABIBCJK $.
      $( [14-Nov-1995] $)

    $( A class of ordered pairs of values in the form used by ~ fvopab4 is a
       function. $)
    funopab4 $p |- Fun { <. x , y >. | ( ph /\ y = A ) } $=
      ( cv wceq wa copab wss wfun simpr ssopab2i funopabeq funss mp2 ) ACEDFZGZ
      BCHZPBCHZISJRJQPBCAPKLBCDMRSNO $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d x y z F $.  $d x y z G $.
    $( The composition of two functions is a function.  Exercise 29 of
       [TakeutiZaring] p. 25.  (The proof was shortened by Andrew Salmon,
       17-Sep-2011.) $)
    funco $p |- ( ( Fun F /\ Fun G ) -> Fun ( F o. G ) ) $=
      ( vx vz vy wfun wa cv wbr wex copab ccom wmo funmo alrimiv moexexv ancoms
      wal syl2an sylibr funopab df-co funeqi ) AFZBFZGZCHDHZBIZUGEHAIZGDJZCEKZF
      ZABLZFUFUJEMZCRULUFUNCUDUIEMZDRZUHDMZUNUEUDUODDEANOCDBNUQUPUNUHUIDEPQSOUJ
      CEUATUMUKCEDABUBUCT $.
      $( [18-Sep-2011] $) $( [26-Jan-1997] $)
  $}

  ${
    $d x y z F $.  $d x y z A $.
    $( A restriction of a function is a function.  Compare Exercise 18 of
       [TakeutiZaring] p. 25. $)
    funres $p |- ( Fun F -> Fun ( F |` A ) ) $=
      ( cres wss wfun wi resss funss ax-mp ) BACZBDBEJEFBAGJBHI $.
      $( [16-Aug-1994] $)
  $}

  ${
    $d x y F $.  $d x y G $.  $d x y A $.
    $( The restriction of a function to the domain of a subclass equals the
       subclass. $)
    funssres $p |- ( ( Fun F /\ G C_ F ) -> ( F |` dom G ) = G ) $=
      ( vx vy wfun wss wa cdm cres wceq cv cop wcel wb wal wi vex wex imp wrel
      ssel opeldm a1i adantl weu funeu2 eldm2 ancrd eximdv syl5bi eupick syl2an
      jcad exp43 com23 com34 pm2.43d imp3a impbid opelres syl6rbbr relres relss
      alrimivv funrel syl5com eqrel sylancr mpbird ) AEZBAFZGZABHZIZBJZCKZDKZLZ
      VNMZVRBMZNZDOCOZVLWACDVLVTVRAMZVPVMMZGZVSVLVTWEVKVTWEPVJVKVTWCWDBAVRUAZVT
      WDPVKVPVQBCQZUBUCUMUDVLWCWDVTVLWCWDVTPVLWCWDWCVTVJVKWCWDWCVTPZPZPVJWCVKWI
      VJWCVKWDWHVJWCGWCDUEWCVTGZDRZWHVKWDGCDAUFVKWDWKWDVTDRVKWKDVPBWGUGVKVTWJDV
      KVTWCWFUHUIUJSWCVTDUKULUNUOSUPUQURUSVPVQAVMDQUTVAVDVLVNTBTZVOWBNAVMVBVJVK
      WLVJATVKWLAVEBAVCVFSCDVNBVGVHVI $.
      $( [19-Mar-2004] $) $( [15-Aug-1994] $)
  $}

  $( Equality of restrictions of a function and a subclass. $)
  fun2ssres $p |- ( ( Fun F /\ G C_ F /\ A C_ dom G ) ->
                  ( F |` A ) = ( G |` A ) ) $=
    ( wfun wss cdm cres wceq resabs1 eqcomd funssres reseq1 syl sylan9eqr 3impa
    wa ) BDZCBEZACFZEZBAGZCAGZHTQRPZUABSGZAGZUBTUEUABASIJUCUDCHUEUBHBCKUDCALMNO
    $.
    $( [2-Jun-2007] $) $( [16-Aug-1994] $)

  ${
    $d x y z F $.  $d x y z G $.
    $( The union of functions with disjoint domains is a function.  Theorem 4.6
       of [Monk1] p. 43. $)
    funun $p |- ( ( ( Fun F /\ Fun G ) /\ ( dom F i^i dom G ) = (/) ) ->
                Fun ( F u. G ) ) $=
      ( vx vy vz wfun wa cdm wceq wrel cv wcel wi wal anim12i wo 19.21bi opeldm
      wn dffun4 cin cun cop funrel relun sylibr adantr elun anbi12i anddi bitri
      c0 disj1 biimpi imnan sylib vex nsyl orel2 syl con2d orel1 orim12d adantl
      syl5bi simprbi 19.21bbi jaao syld alrimiv alrimivv sylanbrc ) AFZBFZGZAHZ
      BHZUAULIZGZABUBZJZCKZDKZUCZVTLZWBEKZUCZVTLZGZWCWFIZMZENZDNCNVTFVOWAVRVOAJ
      ZBJZGWAVMWMVNWNAUDBUDOABUEUFUGVSWLCDVSWKEVSWIWDALZWGALZGZWDBLZWGBLZGZPZWJ
      WIWQWOWSGZPZWRWPGZWTPZPZVSXAWIWOWRPZWPWSPZGXFWEXGWHXHWDABUHWGABUHUIWOWRWP
      WSUJUKVRXFXAMVOVRXCWQXEWTVRXBSXCWQMVRWBVPLZWBVQLZGZXBVRXIXJSMZXKSVRXLCVRX
      LCNCVPVQUMUNQZXIXJUOUPWOXIWSXJWBWCACUQZRWBWFBXNROURXBWQUSUTVRXDSXEWTMVRXJ
      XIGZXDVRXJXISMXOSVRXIXJXMVAXJXIUOUPWRXJWPXIWBWCBXNRWBWFAXNROURXDWTVBUTVCV
      DVEVOXAWJMVRVMWQWJVNWTVMWQWJMZDEVMXPENDNZCVMWMXQCNCDEATVFQVGVNWTWJMZDEVNX
      RENDNZCVNWNXSCNCDEBTVFQVGVHUGVIVJVKCDEVTTVL $.
      $( [12-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    funsn.1 $e |- A e. _V $.
    $( A singleton of an ordered pair is a function.  Theorem 10.5 of [Quine]
       p. 65. $)
    funsn $p |- Fun { <. A , B >. } $=
      ( vx vy cop cv wmo wcel wceq wa moeq moani ax-17 mobid mpbiri mobii mpbir
      cvv vex csn wfun wrel wbr wal dffun6 relsnop wn opprc2 eqeq2d opth syl6bb
      opthg pm2.61i opex elsnc df-br ax-gen mpbir2an ) ABFZUAZUBVAUCDGZEGZVAUDZ
      EHZDUEDEVAUFABCUGVEDVEVBVCFZVAIZEHZVHVFUTJZEHZBSIZVJVKVJVBAJZVCBJZKZEHVMV
      LEEBLMVKVIVNEVKENVBVCABSDTZETZUMOPVKUHZVJVLVCAJZKZEHVRVLEEALMVQVIVSEVQENV
      QVIVFAAFZJVSVQUTVTVFABUIUJVBVCAAVOVPCUKULOPUNVGVIEVFUTVBVCUOUPQRVDVGEVBVC
      VAUQQRURUS $.
      $( [1-Oct-2013] $) $( [12-Aug-1994] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    funsnOLD.1 $e |- A e. _V $.
    funsnOLD.2 $e |- B e. _V $.
    $( A singleton of an ordered pair is a function.  Theorem 10.5 of [Quine]
       p. 65. $)
    funsnOLD $p |- Fun { <. A , B >. } $=
      ( vx vy cop csn wfun wrel wbr wmo wal dffun6 relsnop wceq vex mobii mpbir
      cv wcel wa moeq moani opth opex elsnc df-br ax-gen mpbir2an ) ABGZHZIULJE
      TZFTZULKZFLZEMEFULNABCOUPEUPUMUNGZULUAZFLZUSUQUKPZFLZVAUMAPZUNBPZUBZFLVCV
      BFFBUCUDUTVDFUMUNABEQFQDUERSURUTFUQUKUMUNUFUGRSUOURFUMUNULUHRSUIUJ $.
      $( [18-Sep-2011] $) $( [12-Aug-1994] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A singleton of an ordered pair is a function.  Theorem 10.5 of [Quine]
       p. 65. $)
    funsng $p |- ( A e. V -> Fun { <. A , B >. } ) $=
      ( vx cv cop csn wfun wceq opeq1 sneqd funeqd vex funsn vtoclg ) DEZBFZGZH
      ABFZGZHDACPAIZRTUAQSPABJKLPBDMNO $.
      $( [1-Oct-2013] $) $( [28-Jun-2011] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( A singleton of an ordered pair is a function.  Theorem 10.5 of [Quine]
       p. 65. $)
    funsngOLD $p |- ( ( A e. V /\ B e. W ) -> Fun { <. A , B >. } ) $=
      ( vx vy cv cop csn wfun wceq opeq1 sneqd funeqd opeq2 vex funsn vtocl2g )
      EGZFGZHZIZJATHZIZJABHZIZJEFABCDSAKZUBUDUGUAUCSATLMNTBKZUDUFUHUCUETBAOMNST
      EPQR $.
      $( [28-Jun-2011] $)
  $}

  $( A set of two pairs is a function if their first members are different.
     (Contributed by FL, 26-Jun-2011.) $)
  funprg $p |- ( ( A =/= B /\ A e. V /\ B e. W )
     -> Fun { <. A , C >. , <. B , D >. } ) $=
    ( wne wcel w3a cop csn cun wfun cpr wa cdm cin c0 funsng dmsnop wceq syl5eq
    anim12i 3adant1 ineq12i disjsn2 3ad2ant1 funun syl2anc df-pr funeqi sylibr
    ) ABGZAEHZBFHZIZACJZKZBDJZKZLZMZUQUSNZMUPURMZUTMZOZURPZUTPZQZRUAZVBUNUOVFUM
    UNVDUOVEACESBDFSUCUDUMUNVJUOUMVIAKZBKZQRVGVKVHVLACTBDTUEABUFUBUGURUTUHUIVCV
    AUQUSUJUKUL $.
    $( [1-Oct-2013] $) $( [26-Jun-2011] $)

  $( A set of two pairs is a function if their first members are different.
     (Contributed by FL, 26-Jun-2011.) $)
  funprgOLD $p |- ( ( A =/= B /\ ( A e. V /\ C e. W ) /\ ( B e. X /\ D e. Y ) )
     -> Fun { <. A , C >. , <. B , D >. } ) $=
    ( wcel wa cop wfun csn cdm cin wceq cvv elex anim12i a1i wne w3a cpr cun c0
    funsngOLD syl 3adant1 dmsnop ineq12 syl2anc disjsn2 eqtrd 3ad2ant1 funun wb
    df-pr funeqi mpbird ) ABUAZAEIZCFIZJZBGIZDHIZJZUBZACKZBDKZUCZLZVHMZVIMZUDZL
    ZVGVLLZVMLZJZVLNZVMNZOZUEPZVOVCVFVRUTVCVPVFVQVCAQIZCQIZJVPVAWCVBWDAERCFRSAC
    QQUFUGVFBQIZDQIZJVQVDWEVEWFBGRDHRSBDQQUFUGSUHUTVCWBVFUTWAAMZBMZOZUEUTVSWGPZ
    VTWHPZWAWIPWJUTACUITWKUTBDUITVSWGVTWHUJUKABULUMUNVLVMUOUKVKVOUPVGVJVNVHVIUQ
    URTUS $.
    $( [26-Jun-2011] $)

  ${
    funpr.1 $e |- A e. _V $.
    funpr.2 $e |- B e. _V $.
    $( A function with a domain of two elements.  (Contributed by Jeff Madsen,
       20-Jun-2010.) $)
    funpr $p |- ( A =/= B -> Fun { <. A , C >. , <. B , D >. } ) $=
      ( wne cvv wcel cop cpr wfun funprg mp3an23 ) ABGAHIBHIACJBDJKLEFABCDHHMN
      $.
      $( [1-Oct-2013] $) $( [20-Jun-2010] $)
  $}

  ${
    funprOLD.1 $e |- A e. _V $.
    funprOLD.2 $e |- B e. _V $.
    funprOLD.3 $e |- C e. _V $.
    funprOLD.4 $e |- D e. _V $.
    $( A function with a domain of two elements.  (Contributed by Jeff Madsen,
       20-Jun-2010.) $)
    funprOLD $p |- ( A =/= B -> Fun { <. A , C >. , <. B , D >. } ) $=
      ( funpr ) ABCDEFI $.
      $( [20-Jun-2010] $)
  $}

  ${
    funtp.1 $e |- A e. _V $.
    funtp.2 $e |- B e. _V $.
    funtp.3 $e |- C e. _V $.
    $( A function with a domain of three elements. $)
    funtp $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
                    -> Fun { <. A , D >. , <. B , E >. , <. C , F >. } ) $=
      ( wne cop cpr csn cun wfun wa cdm cin c0 wceq w3a funpr funsn jctir df-pr
      ctp dmprop eqtri dmsnop ineq12i disjsn2 anim12i sylib syl5eq funun syl2an
      undisj1 3impb df-tp funeqi sylibr ) ABJZACJZBCJZUAADKZBEKZLZCFKZMZNZOZVEV
      FVHUFZOVBVCVDVKVBVGOZVIOZPVGQZVIQZRZSTVKVCVDPZVBVMVNABDEGHUBCFIUCUDVRVQAM
      ZBMZNZCMZRZSVOWAVPWBVOABLWAABDEUGABUEUHCFUIUJVRVSWBRSTZVTWBRSTZPWCSTVCWDV
      DWEACUKBCUKULVSVTWBUQUMUNVGVIUOUPURVLVJVEVFVHUSUTVA $.
      $( [1-Oct-2013] $) $( [14-Sep-2011] $)
  $}

  ${
    funtpOLD.1 $e |- A e. _V $.
    funtpOLD.2 $e |- B e. _V $.
    funtpOLD.3 $e |- C e. _V $.
    funtpOLD.4 $e |- D e. _V $.
    funtpOLD.5 $e |- E e. _V $.
    funtpOLD.6 $e |- F e. _V $.
    $( A function with a domain of three elements. $)
    funtpOLD $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
                    -> Fun { <. A , D >. , <. B , E >. , <. C , F >. } ) $=
      ( funtp ) ABCDEFGHIM $.
      $( [14-Sep-2011] $)
  $}

  ${
    fnsn.1 $e |- A e. _V $.
    $( Functionality and domain of the singleton of an ordered pair.
       (Contributed by Jonathan Ben-Naim, 3-Jun-2011.) $)
    fnsn $p |- { <. A , B >. } Fn { A } $=
      ( cop csn wfn wfun cdm wceq df-fn funsn dmsnop mpbir2an ) ABDEZAEZFNGNHOI
      NOJABCKABLM $.
      $( [1-Oct-2013] $) $( [3-Jun-2011] $)
  $}

  ${
    fnsnOLD.1 $e |- A e. _V $.
    fnsnOLD.2 $e |- B e. _V $.
    $( Functionality and domain of the singleton of an ordered pair.
       (Contributed by Jonathan Ben-Naim, 3-Jun-2011.) $)
    fnsnOLD $p |- { <. A , B >. } Fn { A } $=
      ( fnsn ) ABCE $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $( Domain of a function with a domain of two different values.
       (Contributed by FL, 26-Jun-2011.) $)
    fnprg $p |- ( ( A =/= B /\ A e. V /\ B e. W )
         -> { <. A , C >. , <. B , D >. } Fn { A , B } ) $=
      ( wne wcel w3a cop cpr wfun cdm wceq wa wfn funprg dmprop jctir df-fn
      sylibr ) ABGAEHBFHIZACJBDJKZLZUCMABKZNZOUCUEPUBUDUFABCDEFQABCDRSUCUETUA
      $.
      $( [1-Oct-2013] $) $( [26-Jun-2011] $)
  $}

  ${
    $( Domain of a function with a domain of two different values.
       (Contributed by FL, 26-Jun-2011.) $)
    fnprgOLD $p |- ( ( A =/= B /\ ( A e. E /\ C e. F ) /\
      ( B e. G /\ D e. H ) )
         -> { <. A , C >. , <. B , D >. } Fn { A , B } ) $=
      ( wne wcel wa w3a cop cpr wfun cdm wceq wfn funprgOLD dmprop jctir sylibr
      df-fn ) ABIAEJCFJKBGJDHJKLZACMBDMNZOZUEPABNZQZKUEUGRUDUFUHABCDEFGHSABCDTU
      AUEUGUCUB $.
      $( [13-Sep-2011] $) $( [26-Jun-2011] $)
  $}

  ${
    fntp.1 $e |- A e. _V $.
    fntp.2 $e |- B e. _V $.
    fntp.3 $e |- C e. _V $.
    $( A function with a domain of three elements. $)
    fntp $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
          -> { <. A , D >. , <. B , E >. , <. C , F >. } Fn { A , B , C } ) $=
      ( wne w3a cop ctp wfun cdm wceq wa wfn funtp dmtpop jctir df-fn sylibr )
      ABJACJBCJKZADLBELCFLMZNZUEOABCMZPZQUEUGRUDUFUHABCDEFGHISABCDEFTUAUEUGUBUC
      $.
      $( [1-Oct-2013] $) $( [14-Sep-2011] $)
  $}

  ${
    fntpOLD.1 $e |- A e. _V $.
    fntpOLD.2 $e |- B e. _V $.
    fntpOLD.3 $e |- C e. _V $.
    fntpOLD.4 $e |- D e. _V $.
    fntpOLD.5 $e |- E e. _V $.
    fntpOLD.6 $e |- F e. _V $.
    $( A function with a domain of three elements. $)
    fntpOLD $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
          -> { <. A , D >. , <. B , E >. , <. C , F >. } Fn { A , B , C } ) $=
      ( fntp ) ABCDEFGHIM $.
      $( [14-Sep-2011] $)
  $}

  $( The empty set is a function.  Theorem 10.3 of [Quine] p. 65. $)
  fun0 $p |- Fun (/) $=
    ( c0 cop csn wss wfun 0ss 0ex funsn funss mp2 ) AAABCZDKEAEKFAAGHAKIJ $.
    $( [7-Apr-1998] $)

  $( The double converse of a function is a function. $)
  funcnvcnv $p |- ( Fun A -> Fun `' `' A ) $=
    ( ccnv wss wfun wi cnvcnvss funss ax-mp ) ABBZACADIDEAFIAGH $.
    $( [21-Sep-2004] $)

  ${
    $d f g x y z w v A $.  $d x y B $.  $d x y R $.
    $( A simpler equivalence for single-rooted (see ~ funcnv ). $)
    funcnv2 $p |- ( Fun `' A <-> A. y E* x x A y ) $=
      ( ccnv wfun wrel cv wbr wmo wal wa dffun6 relcnv biantrur vex brcnv mobii
      albii 3bitr2i ) CDZETFZBGZAGZTHZAIZBJZKUFUCUBCHZAIZBJBATLUAUFCMNUEUHBUDUG
      AUBUCCBOAOPQRS $.
      $( [9-Aug-2004] $)

    $( The converse of a class is a function iff the class is single-rooted,
       which means that for any ` y ` in the range of ` A ` there is at most
       one ` x ` such that ` x A y ` .  Definition of single-rooted in
       [Enderton] p. 43.  See ~ funcnv2 for a simpler version. $)
    funcnv $p |- ( Fun `' A <-> A. y e. ran A E* x x A y ) $=
      ( cv wbr wmo wal crn wcel wi ccnv wfun wral wa vex pm4.71ri mobii moanimv
      brelrn bitri albii funcnv2 df-ral 3bitr4i ) ADZBDZCEZAFZBGUFCHZIZUHJZBGCK
      LUHBUIMUHUKBUHUJUGNZAFUKUGULAUGUJUEUFCAOBOSPQUJUGARTUAABCUBUHBUIUCUD $.
      $( [13-Aug-2004] $)

    $( A condition showing a class is single-rooted.  (See ~ funcnv ). $)
    funcnv3 $p |- ( Fun `' A <-> A. y e. ran A E! x e. dom A x A y ) $=
      ( cv wbr wmo crn wral wex ccnv wfun cdm wreu wcel abeq2i biimpi biantrurd
      wa dfrn2 weu ralbiia funcnv df-reu vex breldm pm4.71ri eu5 3bitr2i ralbii
      eubii 3bitr4i ) ADZBDZCEZAFZBCGZHUNAIZUORZBUPHCJKUNACLZMZBUPHUOURBUPUMUPN
      ZUQUOVAUQUQBUPABCSOPQUAABCUBUTURBUPUTULUSNZUNRZATUNATURUNAUSUCUNVCAUNVBUL
      UMCAUDUEUFUJUNAUGUHUIUK $.
      $( [26-May-2006] $)

    $( The double converse of a class is a function iff the class is
       single-valued.  Each side is equivalent to Definition 6.4(2) of
       [TakeutiZaring] p. 23, who use the notation "Un(A)" for single-valued.
       Note that ` A ` is not necessarily a function. $)
    fun2cnv $p |- ( Fun `' `' A <-> A. x E* y x A y ) $=
      ( ccnv wfun cv wbr wmo wal funcnv2 vex brcnv mobii albii bitri ) CDZDEBFZ
      AFZPGZBHZAIRQCGZBHZAIBAPJTUBASUABQRCBKAKLMNO $.
      $( [13-Aug-2004] $)

    $( A single-valued relation is a function.  (See ~ fun2cnv for
       "single-valued.") Definition 6.4(4) of [TakeutiZaring] p. 24. $)
    svrelfun $p |- ( Fun A <-> ( Rel A /\ Fun `' `' A ) ) $=
      ( vx vy wfun wrel cv wbr wmo wal wa ccnv dffun6 fun2cnv anbi2i bitr4i ) A
      DAEZBFCFAGCHBIZJPAKKDZJBCALRQPBCAMNO $.
      $( [17-Jan-2006] $)

    $( Single-rootedness (see ~ funcnv ) of a class cut down by a cross
       product. $)
    fncnv $p |- ( `' ( R i^i ( A X. B ) ) Fn B <->
                  A. y e. B E! x e. A x R y ) $=
      ( cxp cin ccnv wfn wfun wceq wa cv wbr wral wcel wmo w3a cvv 3bitr4i wreu
      cdm df-fn df-rn eqeq1i anbi2i wrex rninxp anbi1i funcnv raleq wi biimt wb
      crn vex brinxp2 ax-mp 3ancoma 3anass mobii moanimv bitri syl6rbbr ralbiia
      3bitri syl6bb syl5bb pm5.32i r19.26 ancom reu5 ralbii 3bitr2i ) ECDFGZHZD
      IVPJZVPUBZDKZLVQVOUOZDKZLZAMZBMZENZACUAZBDOZVPDUCWAVSVQVTVRDVOUDUEUFWAVQL
      ZWEACUGZWCCPZWELZAQZLZBDOZWBWGWAWLBDOZLWIBDOZWOLWHWNWAWPWOABCDEUHUIWAVQWO
      VQWCWDVONZAQZBVTOZWAWOABVOUJWAWSWRBDOWOWRBVTDUKWRWLBDWDDPZWLWTWLULZWRWTWL
      UMWRWTWKLZAQXAWQXBAWQWJWTWERZWTWJWERXBWDSPWQXCUNBUPWCWDCDESUQURWJWTWEUSWT
      WJWEUTVFVAWTWKAVBVCVDVEVGVHVIWIWLBDVJTVQWAVKWFWMBDWEACVLVMTVN $.
      $( [5-Mar-2007] $)

    $( Two ways of stating that ` A ` is one-to-one (but not necessarily a
       function).  Each side is equivalent to Definition 6.4(3) of
       [TakeutiZaring] p. 24, who use the notation "Un_2 (A)" for one-to-one
       (but not necessarily a function). $)
    fun11 $p |- ( ( Fun `' `' A /\ Fun `' A ) <->
         A. x A. y A. z A. w ( ( x A y /\ z A w ) -> ( x = z <-> y = w ) ) ) $=
      ( cv wbr wa wceq wal ccnv wfun bi2.04 anbi12i 2albii 19.26-2 alcom 3bitri
      wi albii dfbi2 imbi2i pm4.76 ax-17 breq1 anbi1d imbi1d equsal bitri breq2
      wb 3bitr2i bitr2i wmo fun2cnv mo4 funcnv2 alrot4 3bitr4i ) CFZBFZEGZUTDFZ
      EGZHZVAVCIZSZBJZDJZCJZAFZVCEGZVDHZVKUTIZSZAJZDJCJZHZVKVAEGZVDHZVNVFUKZSZB
      JAJZDJCJZEKZKLZWELZHWBDJCJBJAJWDVHVPHZDJCJVRWCWHCDWCVNVTVFSZSZVFVTVNSZSZH
      ZBJAJWJBJAJZWLBJZAJZHWHWBWMABWBVTVNVFSZVFVNSZHZSVTWQSZVTWRSZHWMWAWSVTVNVF
      UAUBVTWQWRUCWTWJXAWLVTVNVFMVTVFVNMNULOWJWLABPWNVHWPVPWNWJAJZBJVHWJABQXBVG
      BWIVGACVGAUDVNVTVEVFVNVSVBVDVKUTVAEUEUFUGUHTUIWOVOAWKVOBDVOBUDVFVTVMVNVFV
      SVLVDVAVCVKEUJUFUGUHTNROVHVPCDPUMWFVJWGVQWFVBBUNZCJVGDJBJZCJVJCBEUOXCXDCV
      BVDBDVAVCUTEUJUPTXDVICVGBDQTRWGVLAUNZDJVOCJAJZDJZVQADEUQXEXFDVLVDACVKUTVC
      EUEUPTXGVPCJZDJVQXFXHDVOACQTVPDCQUIRNWBABCDURUS $.
      $( [17-Jan-2006] $)

    $( The union of a chain (with respect to inclusion) of functions is a
       function. $)
    fununi $p |- ( A. f e. A ( Fun f /\ A. g e. A ( f C_ g \/ g C_ f ) ) ->
                 Fun U. A ) $=
      ( vx vy vz vw vv cv wfun wss wo wral wa wrel wcel wceq wi wal wex simprbi
      cuni cop funrel adantr ralimi reluni sylibr r19.28av ssel anim1d ax-4 a4s
      dffun4 3syl syl9r adantl anim2d jaod imp funeq sseq1 sseq2 orbi12d anbi2d
      anbi12d cbvral2v ralcom orcom syl5bb bitri anbi12i anidm anandir r19.26-2
      2ralbii bitr2i 3bitr3i eluni eeanv an4 ancom 2exbii 3bitr2i imbi1i 19.23v
      albii impexp 2albii bitr4i 3bitr3ri 3imtr4i alrimiv alrimivv syl sylanbrc
      r2al ) BIZJZWRCIZKZWTWRKZLZCAMZNZBAMZAUBZOZDIZEIZUCZXGPZXIFIZUCZXGPZNZXJX
      MQZRZFSZESDSZXGJXFWROZBAMXHXEYABAWSYAXDWRUDUEUFBAUGUHXFWSXCNZCAMZBAMZXTXE
      YCBAWSXCCAUIUFYDXSDEYDXRFGIZJZHIZJZNZYEYGKZYGYEKZLZNZHAMZGAMZXKYEPZXNYGPZ
      NZXQRZHAMZGAMZYDXRYNYTGAYMYSHAYIYLYSYIYJYSYKYHYJYSRYFYJYRXKYGPZYQNZYHXQYJ
      YPUUBYQYEYGXKUJUKYHUUCXQRZFSZESZDSZUUEUUDYHYGOUUGDEFYGUNUAUUFUUEDUUEEULUM
      UUDFULUOUPUQYFYKYSRYHYKYRYPXNYEPZNZYFXQYKYQUUHYPYGYEXNUJURYFUUIXQRZFSZESZ
      DSZUUKUUJYFYEOUUMDEFYEUNUAUULUUKDUUKEULUMUUJFULUOUPUEUSUTUFUFYDYDNYFYLNZH
      AMGAMZYHYLNZHAMGAMZNZYDYOYDUUOYDUUQYBUUNYFYEWTKZWTYEKZLZNBCGHAAWRYEQZWSYF
      XCUVAWRYEVAUVBXAUUSXBUUTWRYEWTVBWRYEWTVCVDVFWTYGQZUVAYLYFUVCUUSYJUUTYKWTY
      GYEVCWTYGYEVBVDVEVGYDYBBAMCAMUUQYBBCAAVHYBUUPWSYEWRKZWRYEKZLZNCBGHAAWTYEQ
      ZXCUVFWSXCXBXALUVGUVFXAXBVIUVGXBUVDXAUVEWTYEWRVBWTYEWRVCVDVJVEWRYGQZWSYHU
      VFYLWRYGVAUVHUVDYJUVEYKWRYGYEVCWRYGYEVBVDVFVGVKVLYDVMYOUUNUUPNZHAMGAMUURY
      MUVIGHAAYFYHYLVNVPUUNUUPGHAAVOVQVRXRYEAPZYGAPZNZYRNZHTZGTZXQRZUUAXPUVOXQX
      PYPUVJNZGTZYQUVKNZHTZNUVQUVSNZHTGTUVOXLUVRXOUVTGXKAVSHXNAVSVLUVQUVSGHVTUW
      AUVMGHUWAYRUVLNUVMYPUVJYQUVKWAYRUVLWBVKWCWDWEUVMXQRZHSZGSZUVNXQRZGSUUAUVP
      UWCUWEGUVMXQHWFWGUWDUVLYSRZHSGSUUAUWBUWFGHUVLYRXQWHWIYSGHAAWQWJUVNXQGWFWK
      VKWLWMWNWODEFXGUNWP $.
      $( [10-Aug-2004] $)

    $( The union of a chain (with respect to inclusion) of single-rooted sets
       is single-rooted.  (See ~ funcnv for "single-rooted" definition.) $)
    funcnvuni $p |- ( A. f e. A ( Fun `' f /\ A. g e. A ( f C_ g \/ g C_ f ) )
                    -> Fun `' U. A ) $=
      ( vy vx vz vw vv cv ccnv wfun wss wo wral wa wceq wrex wi wal wcel eqeq2d
      cab cnveq cbvrexv funeqd sseq1 sseq2 orbi12d ralbidv anbi12d rcla4v funeq
      biimprcd cnvss orim12i wb sseq12 ancoms syl5ibrcom exp3a syl6com rexlimdv
      cuni com23 alrimdv anim12ii syl5bi alrimiv df-ral vex eqeq1 rexbidv ralab
      elab anbi2i imbi12i albii bitr2i sylib fununi syl ciun cnvuni cnvex eqtri
      dfiun2 ax-mp sylibr ) BIZJZKZWICIZLZWLWILZMZCANZOZBANZDIZEIZJZPZEAQZDUBZV
      CZKZAVCJZKZWRFIZKZXIGIZLZXKXILZMZGXDNZOZFXDNZXFWRXIXAPZEAQZXJXKXAPZEAQZXN
      RZGSZOZRZFSZXQWRYEFXSXIHIZJZPZHAQWRYDXRYIEHAWTYGPXAYHXIWTYGUCUAUDWRYIYDHA
      YGATWRYHKZYGWLLZWLYGLZMZCANZOZYIYDRWQYOBYGAWIYGPZWKYJWPYNYPWJYHWIYGUCUEYP
      WOYMCAYPWMYKWNYLWIYGWLUFWIYGWLUGUHUIUJUKYJYIXJYNYCYIXJYJXIYHULUMYNYIYBGYN
      YAYIXNYNXTYIXNRZEAWTATYNYGWTLZWTYGLZMZXTYQRYMYTCWTAWLWTPYKYRYLYSWLWTYGUGW
      LWTYGUFUHUKYTXTYIXNYTXNXTYIOZYHXALZXAYHLZMYRUUBYSUUCYGWTUNWTYGUNUOUUAXLUU
      BXMUUCYIXTXLUUBUPXIYHXKXAUQURXKXAXIYHUQUHUSUTVAVBVDVEVFVAVBVGVHXQXIXDTZXP
      RZFSYFXPFXDVIUUEYEFUUDXSXPYDXCXSDXIFVJWSXIPXBXREAWSXIXAVKVLVNXOYCXJXCYAXN
      GDWSXKPXBXTEAWSXKXAVKVLVMVOVPVQVRVSXDFGVTWAXGXEPXHXFUPXGEAXAWBXEEAWCEDAXA
      WTEVJWDWFWEXGXEULWGWH $.
      $( [11-Aug-2004] $)

    $( The union of a chain (with respect to inclusion) of one-to-one functions
       is a one-to-one function. $)
    fun11uni $p |- ( A. f e. A ( ( Fun f /\ Fun `' f ) /\
                   A. g e. A ( f C_ g \/ g C_ f ) ) ->
                   ( Fun U. A /\ Fun `' U. A ) ) $=
      ( cv wfun ccnv wa wo wral cuni simpl anim1i ralimi fununi simpr funcnvuni
      wss syl jca ) BDZEZTFEZGZTCDZQUDTQHCAIZGZBAIZAJZEZUHFEZUGUAUEGZBAIUIUFUKB
      AUCUAUEUAUBKLMABCNRUGUBUEGZBAIUJUFULBAUCUBUEUAUBOLMABCPRS $.
      $( [11-Aug-2004] $)
  $}

  ${
    $d x y F $.  $d x y G $.
    $( The intersection with a function is a function.  Exercise 14(a) of
       [Enderton] p. 53.  (The proof was shortened by Andrew Salmon,
       17-Sep-2011.) $)
    funin $p |- ( Fun F -> Fun ( F i^i G ) ) $=
      ( cin wss wfun wi inss1 funss ax-mp ) ABCZADAEJEFABGJAHI $.
      $( [18-Sep-2011] $) $( [19-Mar-2004] $)
  $}

  $( The restriction of a one-to-one function is one-to-one. $)
  funres11 $p |- ( Fun `' F -> Fun `' ( F |` A ) ) $=
    ( cres ccnv wss wfun wi resss cnvss ax-mp funss ) BACZDZBDZEZNFMFGLBEOBAHLB
    IJMNKJ $.
    $( [25-Mar-1998] $)

  $( The converse of a restricted function. $)
  funcnvres $p |- ( Fun `' F -> `' ( F |` A ) = ( `' F |` ( F " A ) ) ) $=
    ( ccnv wfun cima cres cdm df-ima df-rn eqtri reseq2i wceq resss cnvss ax-mp
    crn wss funssres mpan2 syl5req ) BCZDZUABAEZFUABAFZCZGZFZUEUCUFUAUCUDPUFBAH
    UDIJKUBUEUAQZUGUELUDBQUHBAMUDBNOUAUERST $.
    $( [27-Mar-1998] $)

  $( Converse of a restricted identity function.  (Contributed by FL,
     4-Mar-2007.) $)
  cnvresid $p |- `' ( _I |` A ) = ( _I |` A ) $=
    ( cid ccnv wfun cres wceq cnvi eqcomi funi funeq mpbii ax-mp cima funcnvres
    reseq1 imai reseq2i syl6eq ) BCZDZBAEZCZUAFBSFZTSBGHUCBDTIBSJKLTUBSBAMZEZUA
    ABNSBFZUEUAFGUFUEBUDEUASBUDOUDABAPQRLRL $.
    $( [20-Feb-2007] $)

  $( The converse of a restriction of the converse of a function equals the
     function restricted to the image of its converse. $)
  funcnvres2 $p |- ( Fun F -> `' ( `' F |` A ) = ( F |` ( `' F " A ) ) ) $=
    ( wfun ccnv cres cima wceq funcnvcnv funcnvres syl wrel funrel dfrel2 sylib
    reseq1 eqtrd ) BCZBDZAEDZRDZRAFZEZBUAEZQTCSUBGBHARIJQTBGZUBUCGQBKUDBLBMNTBU
    AOJP $.
    $( [4-May-2005] $)

  $( The image of the pre-image of a function. $)
  funimacnv $p |- ( Fun F -> ( F " ( `' F " A ) ) = ( A i^i ran F ) ) $=
    ( wfun ccnv cima cres crn cin funcnvres2 rneqd df-ima syl6reqr df-rn ineq2i
    cdm dmres dfdm4 3eqtr2ri syl6eq ) BCZBBDZAEZEZUAAFZDZGZABGZHZTUFBUBFZGUCTUE
    UIABIJBUBKLUHAUAOZHUDOUFUGUJABMNUAAPUDQRS $.
    $( [25-May-2004] $)

  $( A kind of contraposition law that infers a subclass of an image from a
     pre-image subclass. $)
  funimass1 $p |- ( ( Fun F /\ A C_ ran F ) ->
                 ( ( `' F " A ) C_ B -> A C_ ( F " B ) ) ) $=
    ( ccnv cima wss wfun crn wa imass2 funimacnv wceq dfss biimpi eqcomd sseq1d
    cin sylan9eq syl5ib ) CDAEZBFCTEZCBEZFCGZACHZFZIZAUBFTBCJUFUAAUBUCUEUAAUDQZ
    AACKUEAUGUEAUGLAUDMNORPS $.
    $( [25-May-2004] $)

  $( A kind of contraposition law that infers an image subclass from a subclass
     of a pre-image. $)
  funimass2 $p |- ( ( Fun F /\ A C_ ( `' F " B ) ) -> ( F " A ) C_ B ) $=
    ( ccnv cima wss wfun imass2 crn cin funimacnv sseq2d inss1 sstr2 mpi syl6bi
    imp sylan2 ) ACDBEZFCGZCAEZCSEZFZUABFZASCHTUCUDTUCUABCIZJZFZUDTUBUFUABCKLUG
    UFBFUDBUEMUAUFBNOPQR $.
    $( [4-May-2007] $) $( [25-May-2004] $)

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    $( The image of a difference is the difference of images. $)
    imadif $p |- ( Fun `' F ->
                 ( F " ( A \ B ) ) = ( ( F " A ) \ ( F " B ) ) ) $=
      ( vy vx cdif cima cv wcel wbr wa wex wn exbii wi wmo vex sylib wo elima2
      ccnv wfun anandir 19.40 sylbi wal ax-17 hbe1 hban funmo brcnv mobii sylan
      mopick con2d imnan alrimi ex exancom alnex 3imtr3g anim2d 19.29r sylan2br
      syl5 andi ianor anbi2i an32 pm3.24 intnan anass mtbir bitri 3bitr4i eldif
      biorfi impbid1 anbi1i notbii anbi12i 3bitr4g eqrdv ) CUAZUBZDCABFZGZCAGZC
      BGZFZWEEHZWFIZWKDHZCJZKZELZWMWHIZWMWIIZMZKZWMWGIWMWJIWEWKAIZWKBIZMZKZWNKZ
      ELZXAWNKZELZXBWNKZELZMZKZWPWTWEXFXLXFXHXCWNKZELZKZWEXLXFXGXMKZELXOXEXPEXA
      XCWNUCNXGXMEUDUEWEXNXKXHWEWNXCKZELZXIMZEUFZXNXKWEXRXTWEXRKZXSEWEXREWEEUGX
      QEUHUIYAXBWNMZOXSYAWNXBWEWNEPZXRWNXCOWEWMWKWDJZEPYCDEWDUJYDWNEWMWKCDQZEQU
      KULRWNXCEUNUMUOXBWNUPRUQURWNXCEUSXIEUTZVAVBVEXLXGXSKZELZXFXKXHXTYHYFXGXSE
      VCVDYGXEEXGXCYBSZKXGXCKZXGYBKZSZYGXEXGXCYBVFXSYIXGXBWNVGVHXEYJYLXAXCWNVIY
      KYJYKXAWNYBKZKYMXAWNVJVKXAWNYBVLVMVQVNVONRVRWOXEEWLXDWNWKABVPVSNWQXHWSXKE
      WMCAYETWRXJEWMCBYETVTWAWBEWMCWFYETWMWHWIVPWBWC $.
      $( [24-May-1998] $)
  $}

  $( The image of an intersection is the intersection of images.  (Contributed
     by Paul Chapman, 11-Apr-2009.) $)
  imain $p |- ( Fun `' F ->
                ( F " ( A i^i B ) ) = ( ( F " A ) i^i ( F " B ) ) ) $=
    ( ccnv wfun cdif cima cin imadif difeq2d eqtrd dfin4 imaeq2i 3eqtr4g ) CDEZ
    CAABFZFZGZCAGZSCBGZFZFZCABHZGSTHORSCPGZFUBAPCIOUDUASABCIJKUCQCABLMSTLN $.
    $( [11-Apr-2009] $)

  ${
    $d w B $.  $d x y z w A $.
    $( Axiom of Replacement using abbreviations.  Axiom 39(vi) of [Quine]
       p. 284.  Compare Exercise 9 of [TakeutiZaring] p. 29. $)
    funimaexg $p |- ( ( Fun A /\ B e. C ) -> ( A " B ) e. _V ) $=
      ( vw vx vy vz wcel wfun cima cvv cv wi imaeq2 eleq1d imbi2d wal wex bitri
      wceq cop wrel dffun5 simprbi wa wb ax-17 axrep4 isset dfima3 eqeq2i abeq2
      cab exbii sylibr syl vtoclg impcom ) BCHAIZABJZKHZUSADLZJZKHZMUSVAMDBCVBB
      TZVDVAUSVEVCUTKVBBANOPUSELZFLZUAAHZVGGLZTMFQGREQZVDUSAUBVJEFGAUCUDVJVGVIH
      VFVBHVHUEERZUFFQZGRZVDVHEFGDVHGUGUHVDVIVCTZGRVMGVCUIVNVLGVNVIVKFUMZTVLVCV
      OVIEFAVBUJUKVKFVIULSUNSUOUPUQUR $.
      $( [10-Sep-2006] $)
  $}

  ${
    zfrep5.1 $e |- B e. _V $.
    $( The image of a set under any function is also a set.  Equivalent of
       Axiom of Replacement ~ ax-rep .  Axiom 39(vi) of [Quine] p. 284.
       Compare Exercise 9 of [TakeutiZaring] p. 29. $)
    funimaex $p |- ( Fun A -> ( A " B ) e. _V ) $=
      ( wfun cvv wcel cima funimaexg mpan2 ) ADBEFABGEFCABEHI $.
      $( [17-Nov-2002] $)
  $}

  ${
    $d x z A $.  $d b x y z $.  $d z ph $.
    $( Part of a study of the Axiom of Replacement used by the Isabelle
       prover.  The object PrimReplace is apparently the image of the function
       encoded by ` ph ( x , y ) ` i.e. the class
       ` ( { <. x , y >. | ph } " A ) ` .  If so, we can prove Isabelle's
       "Axiom of Replacement" conclusion without using the Axiom of
       Replacement, for which I (N. Megill) currently have no explanation.
       (The proof was shortened by Andrew Salmon, 17-Sep-2011.) $)
    isarep1 $p |- ( b e. ( { <. x , y >. | ph } " A ) <->
                 E. x e. A [ b / y ] ph ) $=
      ( vz copab cima wcel wbr wrex wsbc vex elima df-br opelopabsb bitri ax-17
      cv cop rexbii hbs1 hbsb wceq sbequ12r sbbid cbvrex 3bitri ) ESZABCGZDHIFS
      ZUIUJJZFDKABUKLZCUILZFDKACUILZBDKFUIUJDEMNULUNFDULUKUITUJIUNUKUIUJOABCFEP
      QUAUNUOFBDUMCEBABFUBUCUOFRUKBSUDZUMACEUPCRAFBUEUFUGUH $.
      $( [18-Sep-2011] $) $( [26-Oct-2006] $)
  $}

  ${
    $d w x y A $.  $d b x y $.  $d y z $.  $d w ph $.  $d z ph $.
    isarep2.1 $e |- A e. _V $.
    isarep2.2 $e |- A. x e. A A. y A. z ( ( ph /\ [ z / y ] ph ) -> y = z ) $.
    $( Part of a study of the Axiom of Replacement used by the Isabelle
       prover.  In Isabelle, the sethood of PrimReplace is apparently
       postulated implicitly by its type signature " ` [ ` i, ` [ ` i, i ` ] `
       => o ` ] ` => i", which automatically asserts that it is a set without
       using any axioms.  To prove that it is a set in Metamath, we need the
       hypotheses of Isabelle's "Axiom of Replacement" as well as the Axiom of
       Replacement in the form ~ funimaex . $)
    isarep2 $p |- E. w w = ( { <. x , y >. | ph } " A ) $=
      ( copab cima cvv wcel cv wceq wex wa cres wmo wi wal resopab imaeq1i wfun
      resima eqtr3i funopab wsbc rspec ax-17 mo3 sylibr moanimv mpgbir funimaex
      mpbir ax-mp eqeltri isset mpbi ) ABCIZFJZKLEMVANEOVABMFLZAPZBCIZFJZKUTFQZ
      FJVAVEUTFUDVFVDFABCFUAUBUEVDUCZVEKLVGVCCRZBVCBCUFVHVBACRZSVBAACDMZUGPCMVJ
      NSDTCTZVIVKBFHUHACDADUIUJUKVBACULUOUMVDFGUNUPUQEVAURUS $.
      $( [26-Oct-2006] $)
  $}

  $( Equality theorem for function predicate with domain. $)
  fneq1 $p |- ( F = G -> ( F Fn A <-> G Fn A ) ) $=
    ( wceq wfun cdm wa wfn funeq dmeq eqeq1d anbi12d df-fn 3bitr4g ) BCDZBEZBFZ
    ADZGCEZCFZADZGBAHCAHOPSRUABCIOQTABCJKLBAMCAMN $.
    $( [1-Aug-1994] $)

  $( Equality theorem for function predicate with domain. $)
  fneq2 $p |- ( A = B -> ( F Fn A <-> F Fn B ) ) $=
    ( wceq wfun cdm wa wfn eqeq2 anbi2d df-fn 3bitr4g ) ABDZCEZCFZADZGNOBDZGCAH
    CBHMPQNABOIJCAKCBKL $.
    $( [1-Aug-1994] $)

  ${
    fneq1d.1 $e |- ( ph -> F = G ) $.
    $( Equality deduction for function predicate with domain.  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    fneq1d $p |- ( ph -> ( F Fn A <-> G Fn A ) ) $=
      ( wceq wfn wb fneq1 syl ) ACDFCBGDBGHEBCDIJ $.
      $( [4-Sep-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function predicate with domain.  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    fneq2d $p |- ( ph -> ( F Fn A <-> F Fn B ) ) $=
      ( wceq wfn wb fneq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [4-Sep-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq12d.1 $e |- ( ph -> F = G ) $.
    fneq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function predicate with domain. $)
    fneq12d $p |- ( ph -> ( F Fn A <-> G Fn B ) ) $=
      ( wfn fneq1d fneq2d bitrd ) ADBHEBHECHABDEFIABCEGJK $.
      $( [26-Jun-2011] $)
  $}

  ${
    fneq1i.1 $e |- F = G $.
    $( Equality inference for function predicate with domain.  (Contributed by
       Paul Chapman, 22-Jun-2011.) $)
    fneq1i $p |- ( F Fn A <-> G Fn A ) $=
      ( wceq wfn wb fneq1 ax-mp ) BCEBAFCAFGDABCHI $.
      $( [4-Sep-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq2i.1 $e |- A = B $.
    $( Equality inference for function predicate with domain. $)
    fneq2i $p |- ( F Fn A <-> F Fn B ) $=
      ( wceq wfn wb fneq2 ax-mp ) ABECAFCBFGDABCHI $.
      $( [4-Sep-2011] $)
  $}

  ${
    $d y F $.  $d y A $.  $d x y $.
    hbfn.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfn.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for a function with domain. $)
    hbfn $p |- ( F Fn A -> A. x F Fn A ) $=
      ( wfn wfun cdm wceq wa df-fn hbfun hbdm hbeq hban hbxfrbi ) DCGDHZDIZCJZK
      ADCLRTAABDEMABBSCABDENFOPQ $.
      $( [30-Jan-2004] $)
  $}

  $( A function with domain is a function. $)
  fnfun $p |- ( F Fn A -> Fun F ) $=
    ( wfn wfun cdm wceq df-fn simplbi ) BACBDBEAFBAGH $.
    $( [1-Aug-1994] $)

  $( A function with domain is a relation. $)
  fnrel $p |- ( F Fn A -> Rel F ) $=
    ( wfn wfun wrel fnfun funrel syl ) BACBDBEABFBGH $.
    $( [1-Aug-1994] $)

  $( The domain of a function. $)
  fndm $p |- ( F Fn A -> dom F = A ) $=
    ( wfn wfun cdm wceq df-fn simprbi ) BACBDBEAFBAGH $.
    $( [2-Aug-1994] $)

  ${
    funfni.1 $e |- ( ( Fun F /\ B e. dom F ) -> ph ) $.
    $( Inference to convert a function and domain antecedent. $)
    funfni $p |- ( ( F Fn A /\ B e. A ) -> ph ) $=
      ( wfn wcel wa wfun cdm fnfun adantr fndm eleq2d biimpar syl2anc ) DBFZCBG
      ZHDIZCDJZGZAQSRBDKLQUARQTBCBDMNOEP $.
      $( [22-Apr-2004] $)
  $}

  $( A function has a unique domain. $)
  fndmu $p |- ( ( F Fn A /\ F Fn B ) -> A = B ) $=
    ( wfn cdm fndm sylan9req ) CADCBDACEBACFBCFG $.
    $( [11-Aug-1994] $)

  $( The first argument of binary relation on a function belongs to the
     function's domain. $)
  fnbr $p |- ( ( F Fn A /\ B F C ) -> B e. A ) $=
    ( wfn wbr cdm wcel cvv wrel fnrel brrelex sylan sylancom fndm eleq2d biimpa
    breldmg syldan ) DAEZBCDFZBDGZHZBAHZTUABIHZUCTDJUAUEADKBCDLMBCIDRNTUCUDTUBA
    BADOPQS $.
    $( [7-May-2004] $)

  $( The first argument of an ordered pair in a function belongs to the
     function's domain. $)
  fnop $p |- ( ( F Fn A /\ <. B , C >. e. F ) -> B e. A ) $=
    ( cop wcel wfn wbr df-br fnbr sylan2br ) BCEDFDAGBCDHBAFBCDIABCDJK $.
    $( [25-Mar-2007] $) $( [8-Aug-1994] $)

  ${
    $d x y F $.  $d x y B $.  $d x A $.
    $( There is exactly one value of a function.  (The proof was shortened by
       Andrew Salmon, 17-Sep-2011.) $)
    fneu $p |- ( ( F Fn A /\ B e. A ) -> E! y B F y ) $=
      ( vx cv wbr weu cdm wcel wfun wceq breq1 eubidv imbi2d wex vex eldm wmo
      wi funmo exmoeu2 syl5ib sylbi vtoclga impcom funfni ) CAFZDGZAHZBCDCDIZJD
      KZUJULEFZUHDGZAHZTZULUJTECUKUMCLZUOUJULUQUNUIAUMCUHDMNOUMUKJUNAPZUPAUMDEQ
      RULUNASURUOEADUAUNAUBUCUDUEUFUG $.
      $( [18-Sep-2011] $) $( [22-Apr-2004] $)

    $( There is exactly one value of a function. $)
    fneu2 $p |- ( ( F Fn A /\ B e. A ) -> E! y <. B , y >. e. F ) $=
      ( wfn wcel wa cv wbr weu cop fneu df-br eubii sylib ) DBECBFGCAHZDIZAJCPK
      DFZAJABCDLQRACPDMNO $.
      $( [7-Nov-1995] $)
  $}

  $( The union of two functions with disjoint domains. $)
  fnun $p |- ( ( ( F Fn A /\ G Fn B ) /\ ( A i^i B ) = (/) ) ->
             ( F u. G ) Fn ( A u. B ) ) $=
    ( wfn wa cin c0 wceq cun wfun cdm wi df-fn ineq12 eqeq1d funun syl6bir dmun
    anbi2d uneq12 syl5eq jctird syl6ibr exp3a impcom an4s syl2anb imp ) CAEZDBE
    ZFABGZHIZCDJZABJZEZUJCKZCLZAIZFDKZDLZBIZFUMUPMZUKCANDBNUQUTUSVBVCUSVBFZUQUT
    FZVCVDVEUMUPVDVEUMFZUNKZUNLZUOIZFUPVDVFVGVIVDVFVEURVAGZHIZFVGVDVKUMVEVDVJUL
    HURAVABOPTCDQRVDVHURVAJUOCDSURAVABUAUBUCUNUONUDUEUFUGUHUI $.
    $( [22-Sep-2004] $)

  ${
    fnunop.x $e |- X e. _V $.
    fnunop.f $e |- F Fn D $.
    fnunop.g $e |- G = ( F u. { <. X , Y >. } ) $.
    fnunop.e $e |- E = ( D u. { X } ) $.
    fnunop.d $e |- -. X e. D $.
    $( Extension of a function with a new ordered pair. $)
    fnunsn $p |- G Fn E $=
      ( wfn cop csn cun wa cin c0 wceq mpbir fnsn pm3.2i wcel disjsn fnun mp2an
      wn fneq1i fneq2i bitri ) DBLZCEFMNZOZAENZOZLZCALZULUNLZPAUNQRSZUPUQURHEFG
      UAUBUSEAUCUGKAEUDTAUNCULUEUFUKUMBLUPBDUMIUHBUOUMJUIUJT $.
      $( [28-Sep-2013] $)
  $}

  $( Composition of two functions. $)
  fnco $p |- ( ( F Fn A /\ G Fn B /\ ran G C_ A ) -> ( F o. G ) Fn B ) $=
    ( wfn crn wss w3a ccom wfun cdm wceq fnfun funco syl2an 3adant3 fndm sseq2d
    wa biimpar dmcosseq syl 3adant2 3ad2ant2 eqtrd df-fn sylanbrc ) CAEZDBEZDFZ
    AGZHZCDIZJZUMKZBLUMBEUHUIUNUKUHCJDJUNUIACMBDMCDNOPULUODKZBUHUKUOUPLZUIUHUKS
    UJCKZGZUQUHUSUKUHURAUJACQRTCDUAUBUCUIUHUPBLUKBDQUDUEUMBUFUG $.
    $( [22-May-2006] $)

  $( A function does not change when restricted to its domain. $)
  fnresdm $p |- ( F Fn A -> ( F |` A ) = F ) $=
    ( wfn wrel cdm wss cres wceq fnrel fndm eqimss syl relssres syl2anc ) BACZB
    DBEZAFZBAGBHABIOPAHQABJPAKLBAMN $.
    $( [5-Sep-2004] $)

  $( A function restricted to a class disjoint with its domain is empty. $)
  fnresdisj $p |- ( F Fn A -> ( ( A i^i B ) = (/) <-> ( F |` B ) = (/) ) ) $=
    ( cres c0 wceq cdm wfn wrel wb relres reldm0 ax-mp dmres incom eqtri ineq1d
    cin fndm syl5eq eqeq1d syl5rbb ) CBDZEFZUCGZEFZCAHZABRZEFUCIUDUFJCBKUCLMUGU
    EUHEUGUECGZBRZUHUEBUIRUJCBNBUIOPUGUIABACSQTUAUB $.
    $( [23-Sep-2004] $)

  $( Membership in two functions restricted by each other's domain. $)
  2elresin $p |- ( ( F Fn A /\ G Fn B ) ->
                 ( ( <. x , y >. e. F /\ <. x , z >. e. G ) <->
                   ( <. x , y >. e. ( F |` ( A i^i B ) ) /\
                     <. x , z >. e. ( G |` ( A i^i B ) ) ) ) ) $=
    ( wfn wa cv cop wcel cin cres fnop anim12i vex opres resss sseli wi 3imtr4i
    an4 elin anbi12d biimprd syl ex pm2.43d impbid1 ) FDHZGEHZIZAJZBJZKZFLZUNCJ
    ZKZGLZIZUPFDEMZNZLZUSGVBNZLZIZUMVAVGUMVAVAVGUAZUMVAIZUNVBLZVHUKUQIZULUTIZIU
    NDLZUNELZIVIVJVKVMVLVNDUNUOFOEUNURGOPUKULUQUTUCUNDEUDUBVJVGVAVJVDUQVFUTUNUO
    FVBBQRUNURGVBCQRUEUFUGUHUIVDUQVFUTVCFUPFVBSTVEGUSGVBSTPUJ $.
    $( [8-Aug-1994] $)

  $( Restriction of a function with a subclass of its domain. $)
  fnssresb $p |- ( F Fn A -> ( ( F |` B ) Fn B <-> B C_ A ) ) $=
    ( cres wfn wfun cdm wceq wa wss df-fn fnfun funres biantrurd ssdmres sseq2d
    syl fndm syl5bbr bitr3d syl5bb ) CBDZBEUBFZUBGBHZIZCAEZBAJZUBBKUFUDUEUGUFUC
    UDUFCFUCACLBCMQNUDBCGZJUFUGBCOUFUHABACRPSTUA $.
    $( [10-Oct-2007] $)

  $( Restriction of a function with a subclass of its domain. $)
  fnssres $p |- ( ( F Fn A /\ B C_ A ) -> ( F |` B ) Fn B ) $=
    ( wfn cres wss fnssresb biimpar ) CADCBEBDBAFABCGH $.
    $( [25-Sep-2004] $) $( [2-Aug-1994] $)

  $( Restriction of a function's domain with an intersection. $)
  fnresin1 $p |- ( F Fn A -> ( F |` ( A i^i B ) ) Fn ( A i^i B ) ) $=
    ( wfn cin wss cres inss1 fnssres mpan2 ) CADABEZAFCKGKDABHAKCIJ $.
    $( [9-Aug-1994] $)

  $( Restriction of a function's domain with an intersection. $)
  fnresin2 $p |- ( F Fn A -> ( F |` ( B i^i A ) ) Fn ( B i^i A ) ) $=
    ( wfn cin wss cres inss2 fnssres mpan2 ) CADBAEZAFCKGKDBAHAKCIJ $.
    $( [9-Aug-1994] $)

  $( Functionality and domain of restricted identity. $)
  fnresi $p |- ( _I |` A ) Fn A $=
    ( cid cres wfn wfun cdm wceq df-fn funi funres ax-mp dmresi mpbir2an ) BACZ
    ADNEZNFAGNAHBEOIABJKALM $.
    $( [27-Aug-2004] $)

  $( The image of a function's domain is its range.  (The proof was shortened
     by Andrew Salmon, 17-Sep-2011.) $)
  fnima $p |- ( F Fn A -> ( F " A ) = ran F ) $=
    ( wfn cima cres crn df-ima fnresdm rneqd syl5eq ) BACZBADBAEZFBFBAGKLBABHIJ
    $.
    $( [18-Sep-2011] $) $( [4-Nov-2004] $)

  ${
    $d x y F $.
    $( A function with empty domain is empty.  (The proof was shortened by
       Andrew Salmon, 17-Sep-2011.) $)
    fn0 $p |- ( F Fn (/) <-> F = (/) ) $=
      ( wfn wceq wrel cdm fnrel fndm reldm0 biimpar syl2anc wfun df-fn fun0 dm0
      c0 mpbir2an fneq1 mpbiri impbii ) AOBZAOCZTADZAEOCZUAOAFOAGUBUAUCAHIJUATO
      OBZUDOKOEOCOOLMNPOAOQRS $.
      $( [18-Sep-2011] $) $( [15-Apr-1998] $)
  $}

  $( A class that is disjoint with the domain of a function has an empty image
     under the function.  (Contributed by FL, 24-Jan-2007.) $)
  fnimadisj $p |- ( ( F Fn A /\ ( A i^i C ) = (/) ) -> ( F " C ) = (/) ) $=
    ( wfn cin c0 wceq wa cdm cima fndm ineq1d eqeq1d biimpar imadisj sylibr ) C
    ADZABEZFGZHCIZBEZFGZCBJFGQUBSQUARFQTABACKLMNCBOP $.
    $( [23-Jan-2007] $)

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    iunfopab.1 $e |- B e. _V $.
    $( Two ways to express a function as a class of ordered pairs.  (The proof
       was shortened by Andrew Salmon, 17-Sep-2011.)  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Sep-2011.) $)
    iunfopab $p |- U_ x e. A { <. x , B >. }
                    = { <. x , y >. | ( x e. A /\ y = B ) } $=
      ( vz cv cop csn wcel wrex cab wceq wa wex ciun copab w3a 3anass exbii
      df-rex elsn anbi2i opeq2 eqeq2d anbi2d ceqsexv 3anrev 3bitr3i bitri abbii
      3bitr2i df-iun df-opab 3eqtr4i ) FGZAGZDHZIZJZACKZFLUPUQBGZHZMZUQCJZVBDMZ
      NZNZBOZAOZFLACUSPVGABQVAVJFVAVEUTNZAOVJUTACUAVKVIAVKVEUPURMZNZVFVEVDNZNZB
      OVIUTVLVEFURUBUCVNVMBDEVFVDVLVEVFVCURUPVBDUQUDUEUFUGVOVHBVFVEVDRVDVEVFRVO
      VHVFVEVDUHVFVEVDSVDVEVFSUITULTUJUKAFCUSUMVGABFUNUO $.
      $( [19-Dec-2008] $)
  $}

  ${
    $d x y z A $.  $d z F $.  $d x y z w $.
    fnopabg.1 $e |- F = { <. x , y >. | ( x e. A /\ ph ) } $.
    $( Functionality and domain of an ordered-pair class abstraction. $)
    fnopabg $p |- ( A. x e. A E! y ph <-> F Fn A ) $=
      ( vz vw weu wral wfn cv wcel wa wceq wmo wi sylibr syl ax-17 wfun cdm wal
      copab hbra1 ra4 eumo imim2i moanimv alrimi funopab wex euex dmopab3 sylib
      ralimi df-fn sylanbrc fneq1i hbopab1 hbxfreq hbfn cop fneu2 hbopab2 opeq2
      hbel eleq1d cbveu eleq2i opabid bitri eubii euanv simprbi ralrimi impbii
      ex ) ACIZBDJZEDKZVTBLZDMZANZBCUDZDKZWAVTWEUAZWEUBDOZWFVTWDCPZBUCWGVTWIBVS
      BDUEVTWCVSQZWIVSBDUFWJWCACPZQWIVSWKWCACUGUHWCACUIRSUJWDBCUKRVTACULZBDJWHV
      SWLBDACUMUPABCDUNUOWEDUQURDEWEFUSRWAVSBDBGDEBGEWEFWDBCGUTVAGLZDMBTVBWAWCV
      SWAWCNZWBCLZVCZEMZCIZVSWNWBWMVCZEMZGIWRGDWBEVDWTWQGCCHGWSEHLWSMCTCGEWEFWD
      BCGVEVAVGWQGTWMWOOWSWPEWMWOWBVFVHVIUOWRWCVSWRWDCIWCVSNWQWDCWQWPWEMWDEWEWP
      FVJWDBCVKVLVMWCACVNVLVOSVRVPVQ $.
      $( [30-Jan-2004] $)
  $}

  ${
    $d x y A $.  $d y B $.
    fnopab2g.1 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    $( Functionality and domain of an ordered-pair class abstraction. $)
    fnopab2g $p |- ( A. x e. A B e. _V <-> F Fn A ) $=
      ( cvv wcel wral cv wceq weu wfn eueq ralbii fnopabg bitri ) DGHZACIBJDKZB
      LZACIECMRTACBDNOSABCEFPQ $.
      $( [23-Mar-2006] $)
  $}

  ${
    $d x y A $.
    fnopab.1 $e |- ( x e. A -> E! y ph ) $.
    fnopab.2 $e |- F = { <. x , y >. | ( x e. A /\ ph ) } $.
    $( Functionality and domain of an ordered-pair class abstraction. $)
    fnopab $p |- F Fn A $=
      ( weu wral wfn rgen fnopabg mpbi ) ACHZBDIEDJNBDFKABCDEGLM $.
      $( [5-Mar-1996] $)
  $}

  ${
    $d x y A $.  $d y B $.
    fnopab2.1 $e |- B e. _V $.
    fnopab2.2 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    $( Functionality and domain of an ordered-pair class abstraction. $)
    fnopab2 $p |- F Fn A $=
      ( cv wceq weu wcel eueq1 a1i fnopab ) BHDIZABCEOBJAHCKBDFLMGN $.
      $( [29-Jan-2004] $)

    $( Domain of an ordered-pair class abstraction that specifies a
       function. $)
    dmopab2 $p |- dom F = A $=
      ( wfn cdm wceq fnopab2 fndm ax-mp ) ECHEICJABCDEFGKCELM $.
      $( [6-Sep-2005] $)
  $}

  $( Equality theorem for functions. $)
  feq1 $p |- ( F = G -> ( F : A --> B <-> G : A --> B ) ) $=
    ( wceq wfn crn wss wa wf fneq1 rneq sseq1d anbi12d df-f 3bitr4g ) CDEZCAFZC
    GZBHZIDAFZDGZBHZIABCJABDJQRUATUCACDKQSUBBCDLMNABCOABDOP $.
    $( [1-Aug-1994] $)

  $( Equality theorem for functions. $)
  feq2 $p |- ( A = B -> ( F : A --> C <-> F : B --> C ) ) $=
    ( wceq wfn crn wss wa wf fneq2 anbi1d df-f 3bitr4g ) ABEZDAFZDGCHZIDBFZQIAC
    DJBCDJOPRQABDKLACDMBCDMN $.
    $( [1-Aug-1994] $)

  $( Equality theorem for functions. $)
  feq3 $p |- ( A = B -> ( F : C --> A <-> F : C --> B ) ) $=
    ( wceq wfn crn wss wa wf sseq2 anbi2d df-f 3bitr4g ) ABEZDCFZDGZAHZIPQBHZIC
    ADJCBDJORSPABQKLCADMCBDMN $.
    $( [1-Aug-1994] $)

  $( Equality theorem for functions.  (Contributed by FL, 14-Jul-2007.)  (The
     proof was shortened by Andrew Salmon, 17-Sep-2011.) $)
  feq23 $p |- ( ( A = C /\ B = D ) -> ( F : A --> B <-> F : C --> D ) ) $=
    ( wceq wf feq2 feq3 sylan9bb ) ACFABEGCBEGBDFCDEGACBEHBDCEIJ $.
    $( [18-Sep-2011] $) $( [14-Jul-2007] $)

  ${
    feq1d.1 $e |- ( ph -> F = G ) $.
    $( Equality deduction for functions. $)
    feq1d $p |- ( ph -> ( F : A --> B <-> G : A --> B ) ) $=
      ( wceq wf wb feq1 syl ) ADEGBCDHBCEHIFBCDEJK $.
      $( [19-Feb-2008] $)
  $}

  ${
    feq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for functions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    feq2d $p |- ( ph -> ( F : A --> C <-> F : B --> C ) ) $=
      ( wceq wf wb feq2 syl ) ABCGBDEHCDEHIFBCDEJK $.
      $( [4-Sep-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    feq12d.1 $e |- ( ph -> F = G ) $.
    feq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for functions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    feq12d $p |- ( ph -> ( F : A --> C <-> G : B --> C ) ) $=
      ( wf feq1d feq2d bitrd ) ABDEIBDFICDFIABDEFGJABCDFHKL $.
      $( [22-Jun-2011] $)
  $}

  ${
    feq1i.1 $e |- F = G $.
    $( Equality inference for functions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    feq1i $p |- ( F : A --> B <-> G : A --> B ) $=
      ( wceq wf wb feq1 ax-mp ) CDFABCGABDGHEABCDIJ $.
      $( [4-Sep-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    feq2i.1 $e |- A = B $.
    $( Equality inference for functions. $)
    feq2i $p |- ( F : A --> C <-> F : B --> C ) $=
      ( wceq wf wb feq2 ax-mp ) ABFACDGBCDGHEABCDIJ $.
      $( [5-Sep-2011] $)
  $}

  ${
    feq23i.1 $e |- A = C $.
    feq23i.2 $e |- B = D $.
    $( Equality inference for functions.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    feq23i $p |- ( F : A --> B <-> F : C --> D ) $=
      ( wceq wf wb feq23 mp2an ) ACHBDHABEICDEIJFGABCDEKL $.
      $( [22-Jun-2011] $)
  $}

  ${
    feq23d.1 $e |- ( ph -> A = C ) $.
    feq23d.2 $e |- ( ph -> B = D ) $.
    $( Equality deduction for functions. $)
    feq23d $p |- ( ph -> ( F : A --> B <-> F : C --> D ) ) $=
      ( wceq wf wb feq23 syl2anc ) ABDICEIBCFJDEFJKGHBCDEFLM $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a mapping. $)
    hbf $p |- ( F : A --> B -> A. x F : A --> B ) $=
      ( wf wfn crn wss wa df-f hbfn hbrn hbss hban hbxfrbi ) CDEIECJZEKZDLZMACD
      ENTUBAABCEFGOABUADABEFPHQRS $.
      $( [29-Jan-2004] $)
  $}

  ${
    elimf.1 $e |- G : A --> B $.
    $( Eliminate a mapping hypothesis for the weak deduction theorem ~ dedth ,
       when a special case ` G : A --> B ` is provable, in order to convert
       ` F : A --> B ` from a hypothesis to an antecedent. $)
    elimf $p |- if ( F : A --> B , F , G ) : A --> B $=
      ( wf cif feq1 elimhyp ) ABCFZABJCDGZFABDFCDABCKHABDKHEI $.
      $( [24-Aug-2006] $)
  $}

  $( A mapping is a function. $)
  ffn $p |- ( F : A --> B -> F Fn A ) $=
    ( wf wfn crn wss df-f simplbi ) ABCDCAECFBGABCHI $.
    $( [2-Aug-1994] $)

  $( Any function is a mapping into ` _V ` .  (The proof was shortened by
     Andrew Salmon, 17-Sep-2011.) $)
  dffn2 $p |- ( F Fn A <-> F : A --> _V ) $=
    ( wfn crn cvv wss wa wf ssv biantru df-f bitr4i ) BACZMBDZEFZGAEBHOMNIJAEBK
    L $.
    $( [18-Sep-2011] $) $( [31-Oct-1995] $)

  $( A mapping is a function. $)
  ffun $p |- ( F : A --> B -> Fun F ) $=
    ( wf wfn wfun ffn fnfun syl ) ABCDCAECFABCGACHI $.
    $( [3-Aug-1994] $)

  $( A mapping is a relation. $)
  frel $p |- ( F : A --> B -> Rel F ) $=
    ( wf wfn wrel ffn fnrel syl ) ABCDCAECFABCGACHI $.
    $( [3-Aug-1994] $)

  $( The domain of a mapping. $)
  fdm $p |- ( F : A --> B -> dom F = A ) $=
    ( wf wfn cdm wceq ffn fndm syl ) ABCDCAECFAGABCHACIJ $.
    $( [2-Aug-1994] $)

  ${
    fdmi.1 $e |- F : A --> B $.
    $( The domain of a mapping. $)
    fdmi $p |- dom F = A $=
      ( wf cdm wceq fdm ax-mp ) ABCECFAGDABCHI $.
      $( [28-Jul-2008] $)
  $}

  $( The range of a mapping. $)
  frn $p |- ( F : A --> B -> ran F C_ B ) $=
    ( wf wfn crn wss df-f simprbi ) ABCDCAECFBGABCHI $.
    $( [3-Aug-1994] $)

  $( A function maps to its range. $)
  dffn3 $p |- ( F Fn A <-> F : A --> ran F ) $=
    ( wfn crn wss wa wf ssid biantru df-f bitr4i ) BACZLBDZMEZFAMBGNLMHIAMBJK
    $.
    $( [1-Sep-1999] $)

  $( Expanding the codomain of a mapping.  (The proof was shortened by Andrew
     Salmon, 17-Sep-2011.) $)
  fss $p |- ( ( F : A --> B /\ B C_ C ) -> F : A --> C ) $=
    ( wss wf wfn crn wa sstr2 com12 anim2d df-f 3imtr4g impcom ) BCEZABDFZACDFZ
    PDAGZDHZBEZISTCEZIQRPUAUBSUAPUBTBCJKLABDMACDMNO $.
    $( [18-Sep-2011] $) $( [10-May-1998] $)

  $( Composition of two mappings.  (The proof was shortened by Andrew Salmon,
     17-Sep-2011.) $)
  fco $p |- ( ( F : B --> C /\ G : A --> B ) -> ( F o. G ) : A --> C ) $=
    ( wfn crn wss wa ccom wf wi fnco 3expib adantr rncoss sstr mpan adantl df-f
    jctird imp anbi12i 3imtr4i ) DBFZDGZCHZIZEAFZEGBHZIZIDEJZAFZULGZCHZIZBCDKZA
    BEKZIACULKUHUKUPUHUKUMUOUEUKUMLUGUEUIUJUMBADEMNOUGUOUEUNUFHUGUODEPUNUFCQRSU
    AUBUQUHURUKBCDTABETUCACULTUD $.
    $( [18-Sep-2011] $) $( [29-Aug-1999] $)

  $( A mapping is a class of ordered pairs.  (The proof was shortened by Andrew
     Salmon, 17-Sep-2011.) $)
  fssxp $p |- ( F : A --> B -> F C_ ( A X. B ) ) $=
    ( wf cdm crn cxp wss wceq fdm eqimss syl xpss12 syl2anc wrel frel relssdmrn
    frn wi sstr2 3syl mpd ) ABCDZCEZCFZGZABGZHZCUGHZUCUDAHZUEBHUHUCUDAIUJABCJUD
    AKLABCRUDAUEBMNUCCOCUFHUHUISABCPCQCUFUGTUAUB $.
    $( [18-Sep-2011] $) $( [3-Aug-1994] $)

  $( Two ways of specifying a partial function from ` A ` to ` B ` . $)
  funssxp $p |- ( ( Fun F /\ F C_ ( A X. B ) ) <->
             ( F : dom F --> B /\ dom F C_ A ) ) $=
    ( wfun cxp wss wa cdm wfn crn funfn biimpi rnss rnxpss sstr sylancl anim12i
    wf df-f jca sylibr dmss dmxpss adantl ffun adantr fssxp ssid mpan2 sylan9ss
    xpss12 impbii ) CDZCABEZFZGZCHZBCRZUQAFZGZUPURUSUPCUQIZCJZBFZGURUMVAUOVCUMV
    ACKLUOVBUNJZFVDBFVCCUNMABNVBVDBOPQUQBCSUAUOUSUMUOUQUNHZFVEAFUSCUNUBABUCUQVE
    AOPUDTUTUMUOURUMUSUQBCUEUFURUSCUQBEZUNUQBCUGUSBBFVFUNFBUHUQABBUKUIUJTUL $.
    $( [13-Nov-2007] $)

  $( A mapping is a partial function. $)
  ffdm $p |- ( F : A --> B -> ( F : dom F --> B /\ dom F C_ A ) ) $=
    ( wf cdm wss fdm feq2d ibir wceq eqimss syl jca ) ABCDZCEZBCDZOAFZNPNOABCAB
    CGZHINOAJQROAKLM $.
    $( [25-Nov-2007] $)

  ${
    opelf.1 $e |- D e. _V $.
    $( The members of an ordered pair element of a mapping belong to the
       mapping's domain and codomain. $)
    opelf $p |- ( ( F : A --> B /\ <. C , D >. e. F ) ->
                ( C e. A /\ D e. B ) ) $=
      ( wf cop wcel wa cxp fssxp sseld opelxp syl6ib imp ) ABEGZCDHZEIZCAIDBIJZ
      QSRABKZITQEUARABELMCDABFNOP $.
      $( [10-Dec-2003] $)
  $}

  $( The union of two functions with disjoint domains. $)
  fun $p |- ( ( ( F : A --> C /\ G : B --> D ) /\ ( A i^i B ) = (/) ) ->
             ( F u. G ) : ( A u. B ) --> ( C u. D ) ) $=
    ( cin c0 wceq wf wa cun wfn crn wss fnun expcom wi rnun df-f unss12 anim12d
    syl5eqss a1i anbi12i an4 bitri 3imtr4g impcom ) ABGHIZACEJZBDFJZKZABLZCDLZE
    FLZJZUJEAMZFBMZKZENZCOZFNZDOZKZKZUPUNMZUPNZUOOZKUMUQUJUTVGVEVIUTUJVGABEFPQV
    EVIRUJVEVHVAVCLUOEFSVACVCDUAUCUDUBUMURVBKZUSVDKZKVFUKVJULVKACETBDFTUEURVBUS
    VDUFUGUNUOUPTUHUI $.
    $( [22-Sep-2004] $)

  $( Composition of two functions. $)
  fnfco $p |- ( ( F Fn A /\ G : B --> A ) -> ( F o. G ) Fn B ) $=
    ( wf wfn crn wss wa ccom df-f fnco 3expb sylan2b ) BADECAFZDBFZDGAHZICDJBFZ
    BADKOPQRABCDLMN $.
    $( [22-May-2006] $)

  $( Restriction of a function with a subclass of its domain. $)
  fssres $p |- ( ( F : A --> B /\ C C_ A ) -> ( F |` C ) : C --> B ) $=
    ( wf wss cres wfn crn df-f fnssres resss rnss ax-mp sstr mpan anim12i an32s
    wa sylanb sylibr ) ABDEZCAFZSDCGZCHZUDIZBFZSZCBUDEUBDAHZDIZBFZSUCUHABDJUIUC
    UKUHUIUCSUEUKUGACDKUFUJFZUKUGUDDFULDCLUDDMNUFUJBOPQRTCBUDJUA $.
    $( [23-Sep-2004] $)

  $( Restriction of a restricted function with a subclass of its domain. $)
  fssres2 $p |- ( ( ( F |` A ) : A --> B /\ C C_ A ) ->
                ( F |` C ) : C --> B ) $=
    ( cres wf wss wa fssres wb resabs1 feq1d adantl mpbid ) ABDAEZFZCAGZHCBOCEZ
    FZCBDCEZFZABCOIQSUAJPQCBRTDCAKLMN $.
    $( [21-Jul-2005] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( Composition of a mapping and restricted identity.  (The proof was
       shortened by Andrew Salmon, 17-Sep-2011.) $)
    fcoi1 $p |- ( F : A --> B -> ( F o. ( _I |` A ) ) = F ) $=
      ( wf wfn cid cres ccom wceq ffn wfun cdm wa df-fn eqimss ccnv cnvi reseq1
      wss syl cnveqi cnvresid eqtr2i coeq2i cores2 syl5eq wrel funrel sylan9eqr
      ax-mp coi1 sylbi ) ABCDCAEZCFAGZHZCIZABCJUMCKZCLZAIZMUPCANUSUQUOCFHZCUSUR
      ASZUOUTIURAOVAUOCFPZAGZPZHUTUNVDCVDUNPUNVCUNVBFIVCUNIQVBFARUJUAAUBUCUDCFA
      UEUFTUQCUGUTCICUHCUKTUIULT $.
      $( [18-Sep-2011] $) $( [13-Dec-2003] $)

    $( Composition of restricted identity and a mapping.  (The proof was
       shortened by Andrew Salmon, 17-Sep-2011.) $)
    fcoi2 $p |- ( F : A --> B -> ( ( _I |` B ) o. F ) = F ) $=
      ( wf wfn crn wss wa cid cres ccom wceq df-f cores wrel coi2 syl sylan9eqr
      fnrel sylbi ) ABCDCAEZCFBGZHIBJCKZCLABCMUBUAUCICKZCICBNUACOUDCLACSCPQRT
      $.
      $( [18-Sep-2011] $) $( [13-Dec-2003] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.  $d y C $.
    $( There is exactly one value of a function in its codomain. $)
    feu $p |- ( ( F : A --> B /\ C e. A ) -> E! y e. B <. C , y >. e. F ) $=
      ( wf wcel wa cv cop weu wreu wfn ffn fneu2 sylan wb vex opelf simprd ex
      pm4.71rd eubidv adantr mpbid df-reu sylibr ) BCEFZDBGZHZAIZCGZDUKJEGZHZAK
      ZUMACLUJUMAKZUOUHEBMUIUPBCENABDEOPUHUPUOQUIUHUMUNAUHUMULUHUMULUHUMHUIULBC
      DUKEARSTUAUBUCUDUEUMACUFUG $.
      $( [10-Dec-2003] $)
  $}

  ${
    $d x y F $.  $d x y A $.  $d x y B $.
    $( The converse of a restriction of a function. $)
    fcnvres $p |- ( F : A --> B -> `' ( F |` A ) = ( `' F |` B ) ) $=
      ( vy vx cv cop cres ccnv wb wal wa wi vex ex pm4.71 sylib opelcnv opelres
      wcel wceq opelf simpld bitri syl6bbr simprd anbi1i bitr3d alrimivv relcnv
      wf wrel relres eqrel mp2an sylibr ) ABCUKZDFZEFZGZCAHZIZTZUTCIZBHZTZJZEKD
      KZVBVEUAZUQVGDEUQUSURGZCTZVCVFUQVKVKUSATZLZVCUQVKVLMVKVMJUQVKVLUQVKLZVLUR
      BTZABUSURCDNZUBZUCOVKVLPQVCVJVATVMURUSVAVPENZRUSURCAVPSUDUEUQVKVKVOLZVFUQ
      VKVOMVKVSJUQVKVOVNVLVOVQUFOVKVOPQVFUTVDTZVOLVSURUSVDBVRSVTVKVOURUSCVPVRRU
      GUDUEUHUIVBULVEULVIVHJVAUJVDBUMDEVBVEUNUOUP $.
      $( [26-Mar-1998] $)
  $}

  $( The pre-image of a class disjoint with a mapping's codomain is empty.
     (Contributed by FL, 24-Jan-2007.) $)
  fimacnvdisj $p |- ( ( F : A --> B /\ ( B i^i C ) = (/) ) ->
    ( `' F " C ) = (/) ) $=
    ( wf cin c0 wceq wa ccnv cdm cima wss crn df-rn frn adantr syl5eqssr ssdisj
    sylancom imadisj sylibr ) ABDEZBCFGHZIZDJZKZCFGHZUFCLGHUCUDUGBMUHUEUGDNZBDO
    UCUIBMUDABDPQRUGBCSTUFCUAUB $.
    $( [23-Jan-2007] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    fint.1 $e |- B =/= (/) $.
    $( Function into an intersection.  (The proof was shortened by Andrew
       Salmon, 17-Sep-2011.) $)
    fint $p |- ( F : A --> |^| B <-> A. x e. B F : A --> x ) $=
      ( wfn crn cint wss wa cv wral wf ssint anbi2i c0 wne wb r19.28zv df-f
      ax-mp bitr4i ralbii 3bitr4i ) DBFZDGZCHZIZJZUEUFAKZIZJZACLZBUGDMBUJDMZACL
      UIUEUKACLZJZUMUHUOUEAUFCNOCPQUMUPREUEUKACSUAUBBUGDTUNULACBUJDTUCUD $.
      $( [18-Sep-2011] $) $( [14-Oct-1999] $)
  $}

  $( Mapping into an intersection.  (The proof was shortened by Andrew Salmon,
     17-Sep-2011.) $)
  fin $p |- ( F : A --> ( B i^i C ) <-> ( F : A --> B /\ F : A --> C ) ) $=
    ( wfn crn cin wss wa wf ssin anbi2i anandi bitr3i df-f anbi12i 3bitr4i ) DA
    EZDFZBCGZHZIZRSBHZIZRSCHZIZIZATDJABDJZACDJZIUBRUCUEIZIUGUJUARSBCKLRUCUEMNAT
    DOUHUDUIUFABDOACDOPQ $.
    $( [18-Sep-2011] $) $( [14-Sep-1999] $)

  ${
    $d A x $.  $d B x $.
    fabexg.1 $e |- F = { x | ( x : A --> B /\ ph ) } $.
    $( Existence of a set of functions.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    fabexg $p |- ( ( A e. C /\ B e. D ) -> F e. _V ) $=
      ( wcel wa cxp cvv cpw xpexg pwexg wss cv cab wf fssxp elpw sylibr eqsstri
      vex anim1i ss2abi ssab2 sstri ssexg mpan 3syl ) CEIDFIJCDKZLIULMZLIZGLIZC
      DEFNULLOGUMPUNUOGBQZUMIZAJZBRZUMGCDUPSZAJZBRUSHVAURBUTUQAUTUPULPUQCDUPTUP
      ULBUDUAUBUEUFUCABUMUGUHGUMLUIUJUK $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d x A $.  $d x B $.
    fabex.1 $e |- A e. _V $.
    fabex.2 $e |- B e. _V $.
    fabex.3 $e |- F = { x | ( x : A --> B /\ ph ) } $.
    $( Existence of a set of functions. $)
    fabex $p |- F e. _V $=
      ( cvv wcel fabexg mp2an ) CIJDIJEIJFGABCDIIEHKL $.
      $( [3-Dec-2007] $)
  $}

  $( If a mapping is a set, its domain is a set.  (The proof was shortened by
     Andrew Salmon, 17-Sep-2011.) $)
  dmfex $p |- ( ( F e. C /\ F : A --> B ) -> A e. _V ) $=
    ( wf wcel cvv cdm wceq wi fdm dmexg eleq1 syl5ib syl impcom ) ABDEZDCFZAGFZ
    QDHZAIZRSJABDKRTGFUASDCLTAGMNOP $.
    $( [18-Sep-2011] $) $( [27-Aug-2006] $)

  $( The empty function. $)
  f0 $p |- (/) : (/) --> A $=
    ( c0 wf wfn crn wss df-f wceq eqid fn0 mpbir rn0 0ss eqsstri mpbir2an ) BAB
    CBBDZBEZAFBABGPBBHBIBJKQBALAMNO $.
    $( [14-Aug-1999] $)

  $( A class is a function with empty codomain iff it and its domain are
     empty. $)
  f00 $p |- ( F : A --> (/) <-> ( F = (/) /\ A = (/) ) ) $=
    ( c0 wf wceq wa wfn wfun cdm ffun crn wss frn ss0 syl dm0rn0 df-fn sylanbrc
    sylibr fn0 sylib fdm eqtr3d jca f0 feq1 feq2 sylan9bb mpbiri impbii ) ACBDZ
    BCEZACEZFZUKULUMUKBCGZULUKBHBIZCEZUOACBJUKBKZCEZUQUKURCLUSACBMURNOBPSZBCQRB
    TUAUKUPACACBUBUTUCUDUNUKCCCDZCUEULUKACCDUMVAACBCUFACCCUGUHUIUJ $.
    $( [10-Dec-2003] $)

  ${
    $d x y A $.  $d x y B $.
    fconst.1 $e |- B e. _V $.
    $( A cross product with a singleton is a constant function.  (The proof was
       shortened by Andrew Salmon, 17-Sep-2011.) $)
    fconst $p |- ( A X. { B } ) : A --> { B } $=
      ( vx vy csn cxp wf wfn crn wss df-f fconstopab fnopab2 rnxpss mpbir2an )
      ABFZAQGZHRAIRJQKAQRLDEABRCDEABMNAQOP $.
      $( [18-Sep-2011] $) $( [14-Aug-1999] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A cross product with a singleton is a constant function. $)
    fconstg $p |- ( B e. V -> ( A X. { B } ) : A --> { B } ) $=
      ( vx cv csn cxp wf wceq wb sneq syl feq1 feq3 sylan9bb syl2anc vex fconst
      xpeq2 vtoclg ) ADEZFZAUBGZHZABFZAUEGZHZDBCUABIZUCUFIZUBUEIZUDUGJUHUJUIUAB
      KZUBUEASLUKUIUDAUBUFHUJUGAUBUCUFMUBUEAUFNOPAUADQRT $.
      $( [19-Oct-2004] $)
  $}

  $( Equality theorem for one-to-one functions. $)
  f1eq1 $p |- ( F = G -> ( F : A -1-1-> B <-> G : A -1-1-> B ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq1 cnveq funeqd anbi12d df-f1 3bitr4g ) CDEZAB
    CFZCGZHZIABDFZDGZHZIABCJABDJQRUATUCABCDKQSUBCDLMNABCOABDOP $.
    $( [10-Feb-1997] $)

  $( Equality theorem for one-to-one functions. $)
  f1eq2 $p |- ( A = B -> ( F : A -1-1-> C <-> F : B -1-1-> C ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq2 anbi1d df-f1 3bitr4g ) ABEZACDFZDGHZIBCDFZQ
    IACDJBCDJOPRQABCDKLACDMBCDMN $.
    $( [10-Feb-1997] $)

  $( Equality theorem for one-to-one functions. $)
  f1eq3 $p |- ( A = B -> ( F : C -1-1-> A <-> F : C -1-1-> B ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq3 anbi1d df-f1 3bitr4g ) ABEZCADFZDGHZICBDFZQ
    ICADJCBDJOPRQABCDKLCADMCBDMN $.
    $( [10-Feb-1997] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf1.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf1.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf1.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a one-to-one function. $)
    hbf1 $p |- ( F : A -1-1-> B -> A. x F : A -1-1-> B ) $=
      ( wf1 wf ccnv wfun wa df-f1 hbf hbcnv hbfun hban hbxfrbi ) CDEICDEJZEKZLZ
      MACDENTUBAABCDEFGHOABUAABEFPQRS $.
      $( [16-May-2004] $)
  $}

  ${
    $d x y F $.
    $( Alternate definition of a one-to-one function. $)
    dff12 $p |- ( F : A -1-1-> B <-> ( F : A --> B /\ A. y E* x x F y ) ) $=
      ( wf1 wf ccnv wfun wa cv wbr wmo wal df-f1 wrel dffun6 relcnv vex bitri
      mpbiran brcnv mobii albii anbi2i ) CDEFCDEGZEHZIZJUFAKZBKZELZAMZBNZJCDEOU
      HUMUFUHUJUIUGLZAMZBNZUMUHUGPUPBAUGQERUAUOULBUNUKAUJUIEBSASUBUCUDTUET $.
      $( [22-Sep-2004] $) $( [31-Dec-1996] $)
  $}

  $( A one-to-one mapping is a mapping. $)
  f1f $p |- ( F : A -1-1-> B -> F : A --> B ) $=
    ( wf1 wf ccnv wfun df-f1 simplbi ) ABCDABCECFGABCHI $.
    $( [31-Dec-1996] $)

  $( A one-to-one mapping is a function on its domain. $)
  f1fn $p |- ( F : A -1-1-> B -> F Fn A ) $=
    ( wf1 wf wfn f1f ffn syl ) ABCDABCECAFABCGABCHI $.
    $( [8-Mar-2014] $)

  $( A one-to-one mapping is a function. $)
  f1fun $p |- ( F : A -1-1-> B -> Fun F ) $=
    ( wf1 wfn wfun f1fn fnfun syl ) ABCDCAECFABCGACHI $.
    $( [8-Mar-2014] $)

  $( A one-to-one onto mapping is a relation. $)
  f1rel $p |- ( F : A -1-1-> B -> Rel F ) $=
    ( wf1 wfn wrel f1fn fnrel syl ) ABCDCAECFABCGACHI $.
    $( [8-Mar-2014] $)

  $( The domain of a one-to-one mapping. $)
  f1dm $p |- ( F : A -1-1-> B -> dom F = A ) $=
    ( wf1 wfn cdm wceq f1fn fndm syl ) ABCDCAECFAGABCHACIJ $.
    $( [8-Mar-2014] $)

  ${
    $( A function that is one-to-one is also one-to-one on some superset of its
       range.  (Contributed by Mario Carneiro, 12-Jan-2013.) $)
    f1ss $p |- ( ( F : A -1-1-> B /\ B C_ C ) -> F : A -1-1-> C ) $=
      ( wf1 wss wa wf ccnv wfun f1f fss sylan df-f1 simprbi adantr sylanbrc ) A
      BDEZBCFZGACDHZDIJZACDERABDHZSTABDKABCDLMRUASRUBUAABDNOPACDNQ $.
      $( [12-Jan-2013] $)
  $}

  $( Two ways to express that a set ` A ` (not necessarily a function) is
     one-to-one.  Each side is equivalent to Definition 6.4(3) of
     [TakeutiZaring] p. 24, who use the notation "Un_2 (A)" for one-to-one.  We
     do not introduce a separate notation since we rarely use it. $)
  f1cnvcnv $p |- ( `' `' A : dom A -1-1-> _V
             <-> ( Fun `' A /\ Fun `' `' A ) ) $=
    ( cdm cvv ccnv wf1 wf wfun wa df-f1 wfn dffn2 wceq dmcnvcnv mpbiran2 bitr3i
    df-fn wb wrel relcnv dfrel2 mpbi funeq ax-mp anbi12i ancom 3bitri ) ABZCADZ
    DZEUGCUIFZUIDZGZHUIGZUHGZHUNUMHUGCUIIUJUMULUNUJUIUGJZUMUGUIKUOUMUIBUGLUIUGP
    AMNOUKUHLZULUNQUHRUPASUHTUAUKUHUBUCUDUMUNUEUF $.
    $( [13-Aug-2004] $)

  $( Composition of one-to-one functions.  Exercise 30 of [TakeutiZaring]
     p. 25. $)
  f1co $p |- ( ( F : B -1-1-> C /\ G : A -1-1-> B ) ->
              ( F o. G ) : A -1-1-> C ) $=
    ( wf ccnv wfun wa ccom wf1 fco funco wceq wb cnvco funeq ax-mp sylibr df-f1
    ancoms anim12i an4s anbi12i 3imtr4i ) BCDFZDGZHZIZABEFZEGZHZIZIACDEJZFZUNGZ
    HZIZBCDKZABEKZIACUNKUFUJUHULURUFUJIUOUHULIUQABCDELULUHUQULUHIUKUGJZHZUQUKUG
    MUPVANUQVBODEPUPVAQRSUAUBUCUSUIUTUMBCDTABETUDACUNTUE $.
    $( [28-May-1998] $)

  $( Equality theorem for onto functions. $)
  foeq1 $p |- ( F = G -> ( F : A -onto-> B <-> G : A -onto-> B ) ) $=
    ( wceq wfn crn wa wfo fneq1 rneq eqeq1d anbi12d df-fo 3bitr4g ) CDEZCAFZCGZ
    BEZHDAFZDGZBEZHABCIABDIPQTSUBACDJPRUABCDKLMABCNABDNO $.
    $( [1-Aug-1994] $)

  $( Equality theorem for onto functions. $)
  foeq2 $p |- ( A = B -> ( F : A -onto-> C <-> F : B -onto-> C ) ) $=
    ( wceq wfn crn wa wfo fneq2 anbi1d df-fo 3bitr4g ) ABEZDAFZDGCEZHDBFZPHACDI
    BCDINOQPABDJKACDLBCDLM $.
    $( [1-Aug-1994] $)

  $( Equality theorem for onto functions. $)
  foeq3 $p |- ( A = B -> ( F : C -onto-> A <-> F : C -onto-> B ) ) $=
    ( wceq wfn crn wa wfo eqeq2 anbi2d df-fo 3bitr4g ) ABEZDCFZDGZAEZHOPBEZHCAD
    ICBDINQROABPJKCADLCBDLM $.
    $( [1-Aug-1994] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbfo.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfo.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbfo.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for an onto function. $)
    hbfo $p |- ( F : A -onto-> B -> A. x F : A -onto-> B ) $=
      ( wfo wfn crn wceq wa df-fo hbfn hbrn hbeq hban hbxfrbi ) CDEIECJZEKZDLZM
      ACDENTUBAABCEFGOABBUADABEFPHQRS $.
      $( [16-May-2004] $)
  $}

  $( An onto mapping is a mapping. $)
  fof $p |- ( F : A -onto-> B -> F : A --> B ) $=
    ( wfn crn wceq wa wss wfo wf eqimss anim2i df-fo df-f 3imtr4i ) CADZCEZBFZG
    PQBHZGABCIABCJRSPQBKLABCMABCNO $.
    $( [3-Aug-1994] $)

  $( An onto mapping is a function. $)
  fofun $p |- ( F : A -onto-> B -> Fun F ) $=
    ( wfo wf wfun fof ffun syl ) ABCDABCECFABCGABCHI $.
    $( [29-Mar-2008] $)

  $( An onto mapping is a function on its domain. $)
  fofn $p |- ( F : A -onto-> B -> F Fn A ) $=
    ( wfo wf wfn fof ffn syl ) ABCDABCECAFABCGABCHI $.
    $( [16-Dec-2008] $)

  $( The codomain of an onto function is its range. $)
  forn $p |- ( F : A -onto-> B -> ran F = B ) $=
    ( wfo wfn crn wceq df-fo simprbi ) ABCDCAECFBGABCHI $.
    $( [3-Aug-1994] $)

  $( Alternate definition of an onto function. $)
  dffo2 $p |- ( F : A -onto-> B <-> ( F : A --> B /\ ran F = B ) ) $=
    ( wfo wf crn wceq wa fof forn jca wfn ffn df-fo biimpri sylan impbii ) ABCD
    ZABCEZCFBGZHRSTABCIABCJKSCALZTRABCMRUATHABCNOPQ $.
    $( [22-Mar-2006] $)

  $( The image of the domain of an onto function. $)
  foima $p |- ( F : A -onto-> B -> ( F " A ) = B ) $=
    ( wfo cima crn cdm imadmrn wf wceq fof fdm imaeq2 3syl syl5reqr forn eqtrd
    ) ABCDZCAEZCFZBRTCCGZEZSCHRABCIUAAJUBSJABCKABCLUAACMNOABCPQ $.
    $( [29-Nov-2002] $)

  $( A function maps onto its range. $)
  dffn4 $p |- ( F Fn A <-> F : A -onto-> ran F ) $=
    ( wfn crn wceq wa wfo eqid biantru df-fo bitr4i ) BACZLBDZMEZFAMBGNLMHIAMBJ
    K $.
    $( [10-May-1998] $)

  $( A function maps its domain onto its range. $)
  funforn $p |- ( Fun A <-> A : dom A -onto-> ran A ) $=
    ( wfun cdm wfn crn wfo funfn dffn4 bitri ) ABAACZDJAEAFAGJAHI $.
    $( [23-Jul-2004] $)

  $( An onto function has unique domain and range. $)
  fodmrnu $p |- ( ( F : A -onto-> B /\ F : C -onto-> D ) ->
                ( A = C /\ B = D ) ) $=
    ( wfo wa wceq wfn fofn fndmu syl2an crn forn sylan9req jca ) ABEFZCDEFZGACH
    ZBDHQEAIECISRABEJCDEJACEKLQRBEMDABENCDENOP $.
    $( [5-Nov-2006] $)

  $( Restriction of a function. $)
  fores $p |- ( ( Fun F /\ A C_ dom F ) ->
              ( F |` A ) : A -onto-> ( F " A ) ) $=
    ( wfun cdm wss cres cima wfo funres anim1i wfn wceq df-fn crn df-ima eqcomi
    wa df-fo mpbiran2 ssdmres anbi2i 3bitr4i sylibr ) BCZABDEZQBAFZCZUEQZABAGZU
    FHZUDUGUEABIJUFAKZUGUFDALZQUJUHUFAMUJUKUFNZUILAUIUFRUIUMBAOPSUEULUGABTUAUBU
    C $.
    $( [4-Mar-1997] $)

  $( Composition of onto functions. $)
  foco $p |- ( ( F : B -onto-> C /\ G : A -onto-> B ) ->
             ( F o. G ) : A -onto-> C ) $=
    ( wf crn wceq ccom wfo fco ad2ant2r cdm fdm eqtr3 sylan rncoeq eqeq1d dffo2
    wa biimpar an32s adantrl jca anbi12i 3imtr4i ) BCDFZDGZCHZTZABEFZEGZBHZTZTZ
    ACDEIZFZUPGZCHZTBCDJZABEJZTACUPJUOUQUSUGUKUQUIUMABCDEKLUJUMUSUKUGUMUIUSUGUM
    TDMZULHZUIUSUGVBBHUMVCBCDNVBULBOPVCUSUIVCURUHCDEQRUAPUBUCUDUTUJVAUNBCDSABES
    UEACUPSUF $.
    $( [22-Mar-2006] $)

  $( A nonzero constant function is onto. $)
  foconst $p |- ( ( F : A --> { B } /\ F =/= (/) ) -> F : A -onto-> { B } ) $=
    ( csn wf c0 wne wa crn wceq wfo wn wrel wb frel relrn0 necon3abid syl wss
    wo frn sssn sylib ord sylbid imdistani dffo2 sylibr ) ABDZCEZCFGZHUJCIZUIJZ
    HAUICKUJUKUMUJUKULFJZLZUMUJCMZUKUONAUICOUPUNCFCPQRUJUNUMUJULUISUNUMTAUICUAU
    LBUBUCUDUEUFAUICUGUH $.
    $( [12-Jan-2007] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq1 $p |- ( F = G -> ( F : A -1-1-onto-> B <-> G : A -1-1-onto-> B ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq1 foeq1 anbi12d df-f1o 3bitr4g ) CDEZABCFZABCGZH
    ABDFZABDGZHABCIABDIOPRQSABCDJABCDKLABCMABDMN $.
    $( [10-Feb-1997] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq2 $p |- ( A = B -> ( F : A -1-1-onto-> C <-> F : B -1-1-onto-> C ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq2 foeq2 anbi12d df-f1o 3bitr4g ) ABEZACDFZACDGZH
    BCDFZBCDGZHACDIBCDIOPRQSABCDJABCDKLACDMBCDMN $.
    $( [10-Feb-1997] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq3 $p |- ( A = B -> ( F : C -1-1-onto-> A <-> F : C -1-1-onto-> B ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq3 foeq3 anbi12d df-f1o 3bitr4g ) ABEZCADFZCADGZH
    CBDFZCBDGZHCADICBDIOPRQSABCDJABCDKLCADMCBDMN $.
    $( [10-Feb-1997] $)

  $( Equality theorem for one-to-one onto functions.  (Contributed by FL,
     14-Jul-2012.) $)
  f1oeq23 $p |- ( ( A = B /\ C = D ) ->
   ( F : A -1-1-onto-> C <-> F : B -1-1-onto-> D ) ) $=
    ( wceq wf1o f1oeq2 f1oeq3 sylan9bb ) ABFACEGBCEGCDFBDEGABCEHCDBEIJ $.
    $( [14-Jul-2012] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf1o.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf1o.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf1o.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a one-to-one onto function. $)
    hbf1o $p |- ( F : A -1-1-onto-> B -> A. x F : A -1-1-onto-> B ) $=
      ( wf1o wf1 wfo wa df-f1o hbf1 hbfo hban hbxfrbi ) CDEICDEJZCDEKZLACDEMRSA
      ABCDEFGHNABCDEFGHOPQ $.
      $( [16-May-2004] $)
  $}

  $( A one-to-one onto mapping is a one-to-one mapping. $)
  f1of1 $p |- ( F : A -1-1-onto-> B -> F : A -1-1-> B ) $=
    ( wf1o wf1 wfo df-f1o simplbi ) ABCDABCEABCFABCGH $.
    $( [12-Dec-2003] $)

  $( A one-to-one onto mapping is a mapping. $)
  f1of $p |- ( F : A -1-1-onto-> B -> F : A --> B ) $=
    ( wf1o wf1 wf f1of1 f1f syl ) ABCDABCEABCFABCGABCHI $.
    $( [12-Dec-2003] $)

  $( A one-to-one onto mapping is function on its domain. $)
  f1ofn $p |- ( F : A -1-1-onto-> B -> F Fn A ) $=
    ( wf1o wf wfn f1of ffn syl ) ABCDABCECAFABCGABCHI $.
    $( [12-Dec-2003] $)

  $( A one-to-one onto mapping is a function. $)
  f1ofun $p |- ( F : A -1-1-onto-> B -> Fun F ) $=
    ( wf1o wfn wfun f1ofn fnfun syl ) ABCDCAECFABCGACHI $.
    $( [12-Dec-2003] $)

  $( A one-to-one onto mapping is a relation. $)
  f1orel $p |- ( F : A -1-1-onto-> B -> Rel F ) $=
    ( wf1o wfun wrel f1ofun funrel syl ) ABCDCECFABCGCHI $.
    $( [13-Dec-2003] $)

  $( The domain of a one-to-one onto mapping. $)
  f1odm $p |- ( F : A -1-1-onto-> B -> dom F = A ) $=
    ( wf1o wfn cdm wceq f1ofn fndm syl ) ABCDCAECFAGABCHACIJ $.
    $( [8-Mar-2014] $)

  $( Alternate definition of one-to-one onto function.  (The proof was
     shortened by Andrew Salmon, 22-Oct-2011.) $)
  dff1o2 $p |- ( F : A -1-1-onto-> B
        <-> ( F Fn A /\ Fun `' F /\ ran F = B ) ) $=
    ( wf1o wf1 wfo wa wf ccnv wfun wfn crn w3a df-f1o df-f1 df-fo anbi12i ancom
    wceq 3anass an12 bitri anbi1i bitr4i wss eqimss df-f biimpri sylan2 3adant2
    anass pm4.71i 3bitr4i 3bitri ) ABCDABCEZABCFZGABCHZCIJZGZCAKZCLZBSZGZGZUTUR
    VBMZABCNUOUSUPVCABCOABCPQUQURVCGZGZVEUQGZVDVEVGVFUQGVHUQVFRVEVFUQVEUTURVBGG
    VFUTURVBTUTURVBUAUBUCUDUQURVCUKVEUQUTVBUQURVBUTVABUEZUQVABUFUQUTVIGABCUGUHU
    IUJULUMUN $.
    $( [22-Oct-2011] $) $( [10-Feb-1997] $)

  $( Alternate definition of one-to-one onto function.  (The proof was
     shortened by Andrew Salmon, 22-Oct-2011.) $)
  dff1o3 $p |- ( F : A -1-1-onto-> B <-> ( F : A -onto-> B /\ Fun `' F ) ) $=
    ( wfn ccnv wfun crn wceq w3a wf1o wfo df-3an an32 bitri dff1o2 df-fo anbi1i
    wa 3bitr4i ) CADZCEFZCGBHZIZTUBRZUARZABCJABCKZUARUCTUARUBRUETUAUBLTUAUBMNAB
    COUFUDUAABCPQS $.
    $( [22-Oct-2011] $) $( [25-Mar-1998] $)

  $( A one-to-one onto function is an onto function. $)
  f1ofo $p |- ( F : A -1-1-onto-> B -> F : A -onto-> B ) $=
    ( wf1o wfo ccnv wfun dff1o3 simplbi ) ABCDABCECFGABCHI $.
    $( [28-Apr-2004] $)

  $( Alternate definition of one-to-one onto function.  (The proof was
     shortened by Andrew Salmon, 22-Oct-2011.) $)
  dff1o4 $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ `' F Fn B ) ) $=
    ( wf1o wfn ccnv wfun crn w3a wa dff1o2 3anass cdm df-rn eqeq1i anbi2i df-fn
    wceq bitr4i 3bitri ) ABCDCAEZCFZGZCHZBRZIUAUCUEJZJUAUBBEZJABCKUAUCUELUFUGUA
    UFUCUBMZBRZJUGUEUIUCUDUHBCNOPUBBQSPT $.
    $( [22-Oct-2011] $) $( [25-Mar-1998] $)

  $( Alternate definition of one-to-one onto function.  (The proof was
     shortened by Andrew Salmon, 22-Oct-2011.) $)
  dff1o5 $p |- ( F : A -1-1-onto-> B <-> ( F : A -1-1-> B /\ ran F = B ) ) $=
    ( wf1o wf1 wfo wa crn wceq df-f1o wf wb f1f ibar syl dffo2 syl6rbbr pm5.32i
    bitri ) ABCDABCEZABCFZGTCHBIZGABCJTUAUBTUBABCKZUBGZUATUCUBUDLABCMUCUBNOABCP
    QRS $.
    $( [22-Oct-2011] $) $( [10-Dec-2003] $)

  $( A one-to-one function maps onto its range. $)
  f1orn $p |- ( F : A -1-1-onto-> ran F <-> ( F Fn A /\ Fun `' F ) ) $=
    ( wfn ccnv wfun crn wceq w3a wa wf1o df-3an dff1o2 eqid biantru 3bitr4i ) B
    ACZBDEZBFZRGZHPQIZSIARBJTPQSKARBLSTRMNO $.
    $( [13-Aug-2004] $)

  $( A one-to-one function maps one-to-one onto its range. $)
  f1f1orn $p |- ( F : A -1-1-> B -> F : A -1-1-onto-> ran F ) $=
    ( wf1 wfn ccnv wfun crn wf1o f1fn wf df-f1 simprbi f1orn sylanbrc ) ABCDZCA
    ECFGZACHCIABCJPABCKQABCLMACNO $.
    $( [4-Sep-2004] $)

  ${
    $d A f $.  $d B f $.
    f1oabexg.1 $e |- F = { f | ( f : A -1-1-onto-> B /\ ph ) } $.
    $( The class of all 1-1-onto functions mapping one set to another is a
       set.  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    f1oabexg $p |- ( ( A e. C /\ B e. D ) -> F e. _V ) $=
      ( wcel wa cv wf1o cab cvv wf wss f1of anim1i ss2abi eqid sylancr syl5eqel
      fabexg ssexg ) BDICEIJZGBCFKZLZAJZFMZNHUEUIBCUFOZAJZFMZPULNIUINIUHUKFUGUJ
      ABCUFQRSAFBCDEULULTUCUIULNUDUAUB $.
      $( [25-Feb-2008] $)
  $}

  $( The converse of a one-to-one onto function is also one-to-one onto.  (The
     proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
  f1ocnv $p |- ( F : A -1-1-onto-> B -> `' F : B -1-1-onto-> A ) $=
    ( wfn ccnv wa wf1o wrel fnrel wceq dfrel2 fneq1 biimprd sylbi anim2i ancoms
    wi mpcom dff1o4 3imtr4i ) CADZCEZBDZFUCUBEZADZFZABCGBAUBGUCUAUFUAUEUCCHZUAU
    EACIUGUDCJZUAUEQCKUHUEUAAUDCLMNROPABCSBAUBST $.
    $( [22-Oct-2011] $) $( [11-Feb-1997] $)

  $( A relation is a one-to-one onto function iff its converse is a one-to-one
     onto function with domain and range interchanged. $)
  f1ocnvb $p |- ( Rel F ->
                ( F : A -1-1-onto-> B <-> `' F : B -1-1-onto-> A ) ) $=
    ( wrel wf1o ccnv f1ocnv wceq wb dfrel2 f1oeq1 sylbi syl5ib impbid2 ) CDZABC
    EZBACFZEZABCGRABQFZEZOPBAQGOSCHTPICJABSCKLMN $.
    $( [8-Dec-2003] $)

  $( The restriction of a one-to-one function maps one-to-one onto the
     image. $)
  f1ores $p |- ( ( F : A -1-1-> B /\ C C_ A ) -> ( F |` C ) : C -1-1-onto->
            ( F " C ) ) $=
    ( wf ccnv wfun wa wss cima cres wfo wf1 wf1o cdm ffun adantr sseq2d biimpar
    fdm fores syl2anc funres11 anim12i an32s df-f1 anbi1i dff1o3 3imtr4i ) ABDE
    ZDFGZHZCAIZHCDCJZDCKZLZUOFGZHZABDMZUMHCUNUONUJUMUKURUJUMHZUPUKUQUTDGZCDOZIZ
    UPUJVAUMABDPQUJVCUMUJVBACABDTRSCDUAUBCDUCUDUEUSULUMABDUFUGCUNUOUHUI $.
    $( [25-Mar-1998] $)

  $( The converse of a one-to-one-onto restricted function.  (Contributed by
     Paul Chapman, 21-Apr-2008.) $)
  f1orescnv $p |- ( ( Fun `' F /\ ( F |` R ) : R -1-1-onto-> P ) ->
                    ( `' F |` P ) : P -1-1-onto-> R ) $=
    ( ccnv wfun cres wf1o wa f1ocnv adantl wceq wb cima funcnvres df-ima dff1o5
    crn wf1 simprbi syl5eq reseq2d sylan9eq f1oeq1 syl mpbid ) CDZEZBACBFZGZHZA
    BUHDZGZABUFAFZGZUIULUGBAUHIJUJUKUMKULUNLUGUIUKUFCBMZFUMBCNUIUOAUFUIUOUHQZAC
    BOUIBAUHRUPAKBAUHPSTUAUBABUKUMUCUDUE $.
    $( [21-Apr-2008] $)

  $( Pre-image of an image. $)
  f1imacnv $p |- ( ( F : A -1-1-> B /\ C C_ A )
                 -> ( `' F " ( F " C ) ) = C ) $=
    ( wf1 wss wa ccnv cima cres resima wfun wceq df-f1 simprbi adantr funcnvres
    wf 3syl wf1o imaeq1 f1ores f1ocnv crn cdm imadmrn fdm imaeq2 syl5reqr f1ofo
    f1of wfo forn syl eqtrd eqtr3d syl5eqr ) ABDEZCAFZGZDHZDCIZIVAVBJZVBIZCVAVB
    KUTDCJZHZVBIZVDCUTVALZVFVCMVGVDMURVHUSURABDRVHABDNOPCDQVFVCVBUASUTCVBVETVBC
    VFTZVGCMABCDUBCVBVEUCVIVGVFUDZCVIVJVFVFUEZIZVGVFUFVIVBCVFRVKVBMVLVGMVBCVFUK
    VBCVFUGVKVBVFUHSUIVIVBCVFULVJCMVBCVFUJVBCVFUMUNUOSUPUQ $.
    $( [30-Sep-2004] $)

  $( A reverse version of ~ f1imacnv .  (Contributed by Jeffrey Hankins,
     16-Jul-2009.) $)
  foimacnv $p |- ( ( F : A -onto-> B /\ C C_ B )
                 -> ( F " ( `' F " C ) ) = C ) $=
    ( wfo wss ccnv cima cres resima wfun wceq fofun adantr funcnvres2 crn df-rn
    wa cdm syl5eqr imaeq1 3syl wfn resss cnvss ax-mp cnvcnvss sstri funss mpsyl
    df-ima eqtr2i jctir df-fn sylibr dfdm4 forn sseq2d biimpar syl6sseq ssdmres
    sylib df-fo sylanbrc foima syl eqtr3d ) ABDEZCBFZRZDDGZCHZHDVLIZVLHZCDVLJVJ
    VKCIZGZVLHZVNCVJDKZVPVMLVQVNLVHVRVIABDMZNCDOVPVMVLUAUBVJVLCVPEZVQCLVJVPVLUC
    ZVPPZCLVTVJVPKZVPSZVLLZRWAVJWCWEVHWCVIVPDFVHVRWCVPVKGZDVOVKFVPWFFVKCUDVOVKU
    EUFDUGUHVSVPDUIUJNVLVOPWDVKCUKVOQULUMVPVLUNUOVJWBVOSZCVOUPVJCVKSZFWGCLVJCDP
    ZWHVHCWIFVIVHWIBCABDUQURUSDQUTCVKVAVBTVLCVPVCVDVLCVPVEVFVGT $.
    $( [9-Jul-2009] $)

  $( The union of two one-to-one onto functions with disjoint domains and
     ranges. $)
  f1oun $p |- ( ( ( F : A -1-1-onto-> B /\ G : C -1-1-onto-> D )
            /\ ( ( A i^i C ) = (/) /\ ( B i^i D ) = (/) ) )
           -> ( F u. G ) : ( A u. C ) -1-1-onto-> ( B u. D ) ) $=
    ( wf1o wa cin c0 wceq cun wfn ccnv wi dff1o4 fnun ex cnvun fneq1i im2anan9
    sylibr an4s syl2anb syl6ibr imp ) ABEGZCDFGZHZACIJKZBDIJKZHZACLZBDLZEFLZGZU
    IULUOUMMZUONZUNMZHZUPUGEAMZENZBMZHFCMZFNZDMZHULUTOZUHABEPCDFPVAVDVCVFVGVAVD
    HZUJUQVCVFHZUKUSVHUJUQACEFQRVIUKUSVIUKHVBVELZUNMUSBDVBVEQUNURVJEFSTUBRUAUCU
    DUMUNUOPUEUF $.
    $( [26-Mar-1998] $)

  ${
    $d A u v x z $.  $d A u v y $.  $d B u v y $.  $d B u v z $.  $d C u v x $.
    $d D u v $.  $d S u v x $.
    fun11iun.1 $e |- ( x = y -> B = C ) $.
    fun11iun.2 $e |- B e. _V $.
    $( The union of a chain (with respect to inclusion) of one-to-one functions
       is a one-to-one function.  (Contributed by Mario Carneiro,
       20-May-2013.) $)
    fun11iun $p |- ( A. x e. A ( B : D -1-1-> S /\
                       A. y e. A ( B C_ C \/ C C_ B ) ) ->
                     U_ x e. A B : U_ x e. A D -1-1-> S ) $=
      ( vz vu vv wss wa wfun wceq cv wrex wcel wex wf1 wo wral ciun wf ccnv wfn
      crn cdm cab cuni eqeq1 rexbidv elab r19.29 ax-17 hbre1 hbab hbral hban wi
      vex f1eq1 biimparc df-f1 ffun anim1i syl adantlr eqeq2d cbvrexv wb sseq12
      sylbi orbi12d biimprcd expdimp rexlimivw imp sylan an32s adantlll sylan2b
      ancoms ralrimiva jca a1i rexlimi fun11uni simpld dfiun2 funeqi sylibr cop
      hbra1 eldm2 f1dm eleq2d syl5bbr adantr syl6 rexbida eliun rexcom4 3bitr4i
      ra4 exbii 3bitr4g eqrdv df-fn sylanbrc elrn2 cxp f1f fssxp opelxp simprbi
      sseld exlimdv rexlimd syl5bi ssrdv df-f simprd cnveqi ) FGDUAZDEMZEDMZUBZ
      BCUCZNZACUCZACFUDZGACDUDZUEZYNUFZOZYMGYNUAYLYNYMUGZYNUHZGMYOYLYNOZYNUIZYM
      PYRYLJQZDPZACRZJUJZUKZOZYTYLUUGUUFUFZOZYLKQZOZUUJUFOZNZUUJLQZMZUUNUUJMZUB
      ZLUUEUCZNZKUUEUCUUGUUINYLUUSKUUEUUJUUESYLUUJDPZACRZUUSUUDUVAJUUJKVBZUUBUU
      JPUUCUUTACUUBUUJDULUMUNYLUVANYKUUTNZACRUUSYKUUTACUOUVCUUSACUUMUURAUUMAUPU
      UQALUUEUUDAJLUUCACUQURUUQAUPUSUTUVCUUSVAAQZCSZUVCUUMUURYFUUTUUMYJYFUUTNFG
      UUJUAZUUMUUTUVFYFFGUUJDVCVDUVFFGUUJUEZUULNUUMFGUUJVEUVGUUKUULFGUUJVFVGVNV
      HVIUVCUUQLUUEUUNUUESUVCUUNDPZACRZUUQUUDUVIJUUNLVBZUUBUUNPUUCUVHACUUBUUNDU
      LUMUNUVIUVCUUNEPZBCRZUUQUVHUVKABCUVDBQPDEUUNHVJVKYJUUTUVLUUQYFYJUVLUUTUUQ
      YJUVLNYIUVKNZBCRZUUTUUQYIUVKBCUOUVNUUTUUQUVMUUTUUQVABCYIUVKUUTUUQUVKUUTNZ
      UUQYIUVOUUOYGUUPYHUUTUVKUUOYGVLUUJDUUNEVMWDUUNEUUJDVMVOVPVQVRVSVTWAWBWCWC
      WEWFWGWHVHWCWEUUEKLWIVHZWJYNUUFAJCDIWKZWLWMYLKUUAYMYLUUJUUNWNZDSZLTZACRZU
      UJFSZACRUUJUUASZUUJYMSYLUVTUWBACYKACWOZYLUVEUVTUWBVLZYLUVEYKUWEYKACXFZYFU
      WEYJUVTUUJDUIZSYFUWBLUUJDUVBWPYFUWGFUUJFGDWQWRWSWTXAVSXBUVRYNSZLTUVSACRZL
      TUWCUWAUWHUWILAUVRCDXCZXGLUUJYNUVBWPUVSALCXDXEAUUJCFXCXHXIYNYMXJXKYLLYSGU
      UNYSSZUVSKTZACRZYLUUNGSZUWHKTUWIKTUWKUWMUWHUWIKUWJXGKUUNYNUVJXLUVSAKCXDXE
      YLUWLUWNACUWDUWNAUPYLUVEYKUWLUWNVAZUWFYFUWOYJYFUVSUWNKYFUVSUVRFGXMZSZUWNY
      FDUWPUVRYFFGDUEDUWPMFGDXNFGDXOVHXRUWQUWBUWNUUJUUNFGUVJXPXQXAXSWTXAXTYAYBY
      MGYNYCXKYLUUIYQYLUUGUUIUVPYDYPUUHYNUUFUVQYEWLWMYMGYNVEXK $.
      $( [20-May-2013] $)
  $}

  $( The restriction of a one-to-one onto function to a difference maps onto
     the difference of the images.  (Contributed by Paul Chapman,
     11-Apr-2009.) $)
  resdif $p |- ( ( Fun `' F /\ ( F |` A ) : A -onto-> C /\
                               ( F |` B ) : B -onto-> D ) ->
                 ( F |` ( A \ B ) ) : ( A \ B ) -1-1-onto-> ( C \ D ) ) $=
    ( ccnv wfun cres wfo cdif cima wf1o wss wceq syl wb ax-mp crn df-ima wa w3a
    cdm fofun difss wf fof fdm sseq2d mpbiri fores syl2anc resres indif reseq2i
    eqtri foeq1 rneqi 3eqtr4i foeq3 bitri funres11 dff1o3 biimpri syl2an ancoms
    cin sylib 3adant3 syl5eq anim12i imadif difeq1 difeq2 sylan9eq sylan2 3impb
    forn f1oeq3 mpbid ) EFGZACEAHZIZBDEBHZIZUAZABJZEWFKZEWFHZLZWFCDJZWHLZVTWBWI
    WDWBVTWIWBWFWGWHIZWHFGZWIVTWBWFWAWFKZWAWFHZIZWLWBWAGWFWAUBZMZWPACWAUCWBWRWF
    AMABUDWBWQAWFWBACWAUEWQANACWAUFACWAUGOUHUIWFWAUJUKWPWFWNWHIZWLWOWHNWPWSPWOE
    AWFVFZHWHEAWFULWTWFEABUMUNUOZWFWNWOWHUPQWNWGNWSWLPWORWHRWNWGWOWHXAUQWAWFSEW
    FSURWNWGWFWHUSQUTVGWFEVAWIWLWMTWFWGWHVBVCVDVEVHWEWGWJNZWIWKPVTWBWDXBWBWDTVT
    EAKZCNZEBKZDNZTZXBWBXDWDXFWBXCWARCEASACWAVQVIWDXEWCRDEBSBDWCVQVIVJVTXGWGXCX
    EJZWJABEVKXDXFXHCXEJWJXCCXEVLXEDCVMVNVNVOVPWGWJWFWHVROVS $.
    $( [11-Apr-2009] $)

  $( The restriction of a one-to-one onto function to an intersection maps onto
     the intersection of the images.  (Contributed by Paul Chapman,
     11-Apr-2009.) $)
  resin $p |- ( ( Fun `' F /\ ( F |` A ) : A -onto-> C /\
                               ( F |` B ) : B -onto-> D ) ->
      ( F |` ( A i^i B ) ) : ( A i^i B ) -1-1-onto-> ( C i^i D ) ) $=
    ( ccnv wfun cres wfo w3a cdif wf1o cin resdif f1ofo syl wceq wb dfin4 ax-mp
    syld3an3 f1oeq3 f1oeq2 reseq2i f1oeq1 3bitrri sylib ) EFGZACEAHIZBDEBHIZJZA
    ABKZKZCCDKZKZEUMHZLZABMZCDMZEURHZLZUHUIUJULUNEULHZIZUQUKULUNVBLVCABCDENULUN
    VBOPAULCUNENUAVAURUOUTLZUMUOUTLZUQUSUOQVAVDRCDSUSUOURUTUBTURUMQVDVERABSZURU
    MUOUTUCTUTUPQVEUQRURUMEVFUDUMUOUTUPUETUFUG $.
    $( [11-Apr-2009] $)

  $( Composition of one-to-one onto functions. $)
  f1oco $p |- ( ( F : B -1-1-onto-> C /\ G : A -1-1-onto-> B ) ->
              ( F o. G ) : A -1-1-onto-> C ) $=
    ( wf1 wfo wa ccom wf1o f1co foco anim12i an4s df-f1o anbi12i 3imtr4i ) BCDF
    ZBCDGZHZABEFZABEGZHZHACDEIZFZACUDGZHZBCDJZABEJZHACUDJRUASUBUGRUAHUESUBHUFAB
    CDEKABCDELMNUHTUIUCBCDOABEOPACUDOQ $.
    $( [19-Mar-1998] $)

  $( The composition of a one-to-one onto function and its converse equals the
     identity relation restricted to the function's range. $)
  f1ococnv2 $p |- ( F : A -1-1-onto-> B -> ( F o. `' F ) = ( _I |` B ) ) $=
    ( wf1o ccnv ccom cid cdm cres wss wceq wf wfun f1of ffun wrel simprbi eqtrd
    df-fun 3syl iss sylib crn f1odm wfo f1ocnv f1ofo forn eqtr4d dmcoeq syl fdm
    reseq2d ) ABCDZCCEZFZGUPHZIZGBIUNUPGJZUPURKUNABCLCMZUSABCNABCOUTCPUSCSQTUPU
    AUBUNUQBGUNUQUOHZBUNCHZUOUCZKUQVAKUNVBAVCABCUDUNBAUODZBAUOUEVCAKABCUFZBAUOU
    GBAUOUHTUICUOUJUKUNVDBAUOLVABKVEBAUONBAUOULTRUMR $.
    $( [13-Dec-2003] $)

  $( The composition of a one-to-one onto function's converse and itself equals
     the identity relation restricted to the function's domain. $)
  f1ococnv1 $p |- ( F : A -1-1-onto-> B -> ( `' F o. F ) = ( _I |` A ) ) $=
    ( wf1o ccnv ccom cres wrel wceq f1orel dfrel2 sylib coeq2d f1ocnv f1ococnv2
    cid syl eqtr3d ) ABCDZCEZTEZFZTCFPAGZSUACTSCHUACIABCJCKLMSBATDUBUCIABCNBATO
    QR $.
    $( [13-Dec-2003] $)

  $( The converse of an injective function is bijective.  (Contributed by FL,
     11-Nov-2011.) $)
  f1cnv $p |- ( F : A -1-1-> B -> `' F : ran F -1-1-onto-> A ) $=
    ( wf1 crn wf1o ccnv f1f1orn f1ocnv syl ) ABCDACEZCFKACGFABCHAKCIJ $.
    $( [11-Nov-2011] $)

  $( Composition of an injective function with its converse.  (Contributed by
     FL, 11-Nov-2011.) $)
  f1cocnv1 $p |- ( F : A -1-1-> B -> ( `' F o. F ) = ( _I |` A ) ) $=
    ( wf1 crn wf1o ccnv ccom cid cres wceq f1f1orn f1ococnv1 syl ) ABCDACEZCFCG
    CHIAJKABCLAOCMN $.
    $( [11-Nov-2011] $)

  $( Composition of an injective function with its converse.  (Contributed by
     FL, 11-Nov-2011.) $)
  f1cocnv2 $p |- ( F : A -1-1-> B -> ( F o. `' F ) = ( _I |` ran F ) ) $=
    ( wf1 crn wf1o ccnv ccom cid cres wceq f1f1orn f1ococnv2 syl ) ABCDACEZCFCC
    GHIOJKABCLAOCMN $.
    $( [11-Nov-2011] $)

  ${
    $d x F $.  $d x A $.  $d x B $.
    f11o.1 $e |- F e. _V $.
    $( Relationship between a mapping and an onto mapping.  Figure 38 of
       [Enderton] p. 145. $)
    ffoss $p |- ( F : A --> B <-> E. x ( F : A -onto-> x /\ x C_ B ) ) $=
      ( wf cv wfo wss wa wex crn wfn df-f dffn4 anbi1i bitri rnex wceq foeq3
      sseq1 anbi12d cla4ev sylbi fof fss sylan exlimiv impbii ) BCDFZBAGZDHZUKC
      IZJZAKZUJBDLZDHZUPCIZJZUOUJDBMZURJUSBCDNUTUQURBDOPQUNUSAUPDERUKUPSULUQUMU
      RUKUPBDTUKUPCUAUBUCUDUNUJAULBUKDFUMUJBUKDUEBUKCDUFUGUHUI $.
      $( [10-May-1998] $)

    $( Relationship between one-to-one and one-to-one onto function. $)
    f11o $p |- ( F : A -1-1-> B <-> E. x ( F : A -1-1-onto-> x /\ x C_ B ) ) $=
      ( wf ccnv wfun wa cv wfo wss wex wf1 wf1o ffoss anbi1i df-f1 dff1o3 bitri
      an32 exbii 19.41v 3bitr4i ) BCDFZDGHZIBAJZDKZUGCLZIZAMZUFIZBCDNBUGDOZUIIZ
      AMZUEUKUFABCDEPQBCDRUOUJUFIZAMULUNUPAUNUHUFIZUIIUPUMUQUIBUGDSQUHUFUIUATUB
      UJUFAUCTUD $.
      $( [4-Apr-1998] $)
  $}

  $( The empty set maps one-to-one into any class. $)
  f10 $p |- (/) : (/) -1-1-> A $=
    ( c0 wf1 wf ccnv wfun df-f1 f0 fun0 wceq wb cnv0 funeq ax-mp mpbir mpbir2an
    ) BABCBABDBEZFZBABGAHRBFZIQBJRSKLQBMNOP $.
    $( [7-Apr-1998] $)

  $( One-to-one onto mapping of the empty set. $)
  f1o00 $p |- ( F : (/) -1-1-onto-> A <-> ( F = (/) /\ A = (/) ) ) $=
    ( c0 wf1o wfn ccnv wceq dff1o4 fn0 biimpi adantr cdm dm0 cnveq syl6eq sylbi
    wa cnv0 fneq1d jca biimpa fndm syl syl5reqr biimpri eqid mpbir fneq2 mpbiri
    sylan9bb impbii bitri ) CABDBCEZBFZAEZQZBCGZACGZQZCABHUPUSUPUQURUMUQUOUMUQB
    IZJKUPCCLZAMUPCAEZVAAGUMUOVBUMAUNCUMUQUNCGUTUQUNCFCBCNROZPSUAACUBUCUDTUSUMU
    OUQUMURUMUQUTUEKUSUOCCEZVDCCGCUFCIUGUQUOVBURVDUQAUNCVCSACCUHUJUITUKUL $.
    $( [15-Apr-1998] $)

  $( Onto mapping of the empty set. $)
  fo00 $p |- ( F : (/) -onto-> A <-> ( F = (/) /\ A = (/) ) ) $=
    ( c0 wfo wf1o wceq wf1 wfn fofn fn0 f10 f1eq1 mpbiri sylbi syl ancri df-f1o
    wa sylibr f1ofo impbii f1o00 bitri ) CABDZCABEZBCFZACFRUDUEUDCABGZUDRUEUDUG
    UDBCHZUGCABIUHUFUGBJUFUGCACGAKCABCLMNOPCABQSCABTUAABUBUC $.
    $( [22-Mar-2006] $)

  $( One-to-one onto mapping of the empty set. $)
  f1o0 $p |- (/) : (/) -1-1-onto-> (/) $=
    ( c0 wf1o wceq f1o00 eqid mpbir2an ) AAABAACZGAADAEZHF $.
    $( [16-Feb-2004] $) $( [10-Sep-2004] $)

  $( A restriction of the identity relation is a one-to-one onto function.
     (The proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
  f1oi $p |- ( _I |` A ) : A -1-1-onto-> A $=
    ( cid cres wf1o wfn ccnv dff1o4 fnresi cnvresid fneq1i mpbir mpbir2an ) AAB
    ACZDMAEZMFZAEZAAMGAHZPNQAOMAIJKL $.
    $( [22-Oct-2011] $) $( [30-Apr-1998] $)

  $( The identity relation is a one-to-one onto function on the universe. $)
  f1ovi $p |- _I : _V -1-1-onto-> _V $=
    ( cvv cid cres wf1o f1oi wceq wb wrel reli dfrel3 mpbi f1oeq1 ax-mp ) AABAC
    ZDZAABDZAENBFZOPGBHQIBJKAANBLMK $.
    $( [16-May-2004] $)

  ${
    f1osn.1 $e |- A e. _V $.
    f1osn.2 $e |- B e. _V $.
    $( A singleton of an ordered pair is one-to-one onto function.  (The proof
       was shortened by Andrew Salmon, 22-Oct-2011.) $)
    f1osn $p |- { <. A , B >. } : { A } -1-1-onto-> { B } $=
      ( csn cop wf1o wfn ccnv dff1o4 fnsn cnvsn fneq1i mpbir mpbir2an ) AEZBEZA
      BFEZGRPHRIZQHZPQRJABCKTBAFEZQHBADKQSUAABCDLMNO $.
      $( [22-Oct-2011] $) $( [18-May-1998] $)
  $}

  ${
    $d A a b $.  $d B b $.
    $( A singleton of an ordered pair is one-to-one onto function.
       (Contributed by Mario Carneiro, 12-Jan-2013.) $)
    f1osng $p |- ( ( A e. V /\ B e. W ) ->
                   { <. A , B >. } : { A } -1-1-onto-> { B } ) $=
      ( va vb cv csn cop wf1o wceq sneq f1oeq2 syl opeq1 f1oeq1 3syl bitrd vex
      wb f1oeq3 opeq2 f1osn vtocl2g ) EGZHZFGZHZUEUGIZHZJZAHZUHAUGIZHZJZULBHZAB
      IZHZJZEFABCDUEAKZUKULUHUJJZUOUTUFULKUKVATUEALUFULUHUJMNUTUIUMKUJUNKVAUOTU
      EAUGOUIUMLULUHUJUNPQRUGBKZUOULUPUNJZUSVBUHUPKUOVCTUGBLUHUPULUNUANVBUMUQKU
      NURKVCUSTUGBAUBUMUQLULUPUNURPQRUEUGESFSUCUD $.
      $( [12-Jan-2013] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    fv2.1 $e |- A e. _V $.
    $( Alternate definition of function value.  Definition 10.11 of [Quine]
       p. 68.  (The proof was shortened by Andrew Salmon, 17-Sep-2011.) $)
    fv2 $p |- ( F ` A ) = U. { x | A. y ( A F y <-> y = x ) } $=
      ( cfv csn cima cv wceq cab cuni wbr wb wal df-fv wcel dfcleq cop vex elsn
      elimasn df-br bitr4i bibi12i albii bitri abbii unieqi eqtri ) CDFDCGHZAIZ
      GZJZAKZLCBIZDMZUPULJZNZBOZAKZLACDPUOVAUNUTAUNUPUKQZUPUMQZNZBOUTBUKUMRVDUS
      BVBUQVCURVBCUPSDQUQDCUPEBTUBCUPDUCUDBULUAUEUFUGUHUIUJ $.
      $( [18-Sep-2011] $) $( [30-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x F $.
    $( A function's value at a proper class is the empty set. $)
    fvprc $p |- ( -. A e. _V -> ( F ` A ) = (/) ) $=
      ( vx cvv wcel wn cfv c0 cuni csn cima cv wceq cab wex wne vex snnz syl6eq
      df-fv df-ne mpbi snprc imaeq2 sylbi ima0 eqeq1d eqcom syl6bb mtbiri nexdv
      abn0 necon1bbii sylib unieqd syl5eq uni0 ) ADEFZABGZHIZHURUSBAJZKZCLZJZMZ
      CNZIUTCABTURVFHURVECOZFVFHMURVECURVEVDHMZVDHPVHFVCCQRVDHUAUBURVEHVDMVHURV
      BHVDURVBBHKZHURVAHMVBVIMAUCVAHBUDUEBUFSUGHVDUHUIUJUKVGVFHVECULUMUNUOUPUQS
      $.
      $( [20-May-1998] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x y F $.
    elfv.1 $e |- B e. _V $.
    $( Membership in a function value. $)
    elfv $p |- ( A e. ( F ` B ) <->
               E. x ( A e. x /\ A. y ( B F y <-> y = x ) ) ) $=
      ( cfv wcel cv wbr wceq wb wal cab cuni wa wex fv2 eleq2i eluniab bitri )
      CDEGZHCDBIZEJUCAIZKLBMZANOZHCUDHUEPAQUBUFCABDEFRSUEACTUA $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.  $d x G $.
    $( Equality theorem for function value. $)
    fveq1 $p |- ( F = G -> ( F ` A ) = ( G ` A ) ) $=
      ( vx wceq csn cima cab cuni cfv imaeq1 eqeq1d abbidv unieqd df-fv 3eqtr4g
      cv ) BCEZBAFZGZDQFZEZDHZICSGZUAEZDHZIABJACJRUCUFRUBUEDRTUDUABCSKLMNDABODA
      COP $.
      $( [29-Dec-1996] $)

    $( Equality theorem for function value. $)
    fveq2 $p |- ( A = B -> ( F ` A ) = ( F ` B ) ) $=
      ( vx wceq csn cima cab cuni cfv sneq imaeq2d eqeq1d abbidv unieqd 3eqtr4g
      cv df-fv ) ABEZCAFZGZDQFZEZDHZICBFZGZUBEZDHZIACJBCJSUDUHSUCUGDSUAUFUBSTUE
      CABKLMNODACRDBCRP $.
      $( [29-Dec-1996] $)
  $}

  ${
    fveq1i.1 $e |- F = G $.
    $( Equality inference for function value. $)
    fveq1i $p |- ( F ` A ) = ( G ` A ) $=
      ( wceq cfv fveq1 ax-mp ) BCEABFACFEDABCGH $.
      $( [2-Sep-2003] $)
  $}

  ${
    fveq1d.1 $e |- ( ph -> F = G ) $.
    $( Equality deduction for function value. $)
    fveq1d $p |- ( ph -> ( F ` A ) = ( G ` A ) ) $=
      ( wceq cfv fveq1 syl ) ACDFBCGBDGFEBCDHI $.
      $( [2-Sep-2003] $)
  $}

  ${
    fveq2i.1 $e |- A = B $.
    $( Equality inference for function value. $)
    fveq2i $p |- ( F ` A ) = ( F ` B ) $=
      ( wceq cfv fveq2 ax-mp ) ABEACFBCFEDABCGH $.
      $( [28-Jul-1999] $)
  $}

  ${
    fveq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function value. $)
    fveq2d $p |- ( ph -> ( F ` A ) = ( F ` B ) ) $=
      ( wceq cfv fveq2 syl ) ABCFBDGCDGFEBCDHI $.
      $( [29-May-1999] $)
  $}

  ${
    fveq12d.1 $e |- ( ph -> F = G ) $.
    fveq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function value.  (Contributed by FL,
       22-Dec-2008.) $)
    fveq12d $p |- ( ph -> ( F ` A ) = ( G ` B ) ) $=
      ( cfv fveq1d fveq2d eqtrd ) ABDHBEHCEHABDEFIABCEGJK $.
      $( [1-Dec-2010] $) $( [22-Dec-2008] $)
  $}

  ${
    $d y z F $.  $d y z A $.  $d x y z $.
    hbfv.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfv.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for function value. $)
    hbfv $p |- ( y e. ( F ` A ) -> A. x y e. ( F ` A ) ) $=
      ( vz cfv csn cima cv wceq cab cuni df-fv hbsn hbima wcel ax-17 hbeq hbuni
      hbab hbxfreq ) ABCDHDCIZJZGKIZLZGMZNGCDOABUHUGAGBABBUEUFABDUDEABCFPQBKUFR
      ASTUBUAUC $.
      $( [14-Nov-1995] $)
  $}

  ${
    $d y z A $.  $d y z F $.  $d y ph $.  $d x y z $.
    hbfvd.1 $e |- ( ph -> A. x ph ) $.
    hbfvd.2 $e |- ( ph -> ( y e. F -> A. x y e. F ) ) $.
    hbfvd.3 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbfv .  If a
       closed theorem version is desired, see ~ hbfvd2 . $)
    hbfvd $p |- ( ph -> ( y e. ( F ` A ) -> A. x y e. ( F ` A ) ) ) $=
      ( vz cv wcel wal cab cfv wi hba1 hbab wceq alrimiv abidhb hbfv a1i fveq1d
      syl fveq2d eqtrd eleq2d albid 3imtr3d ) ACJZIJZDKZBLZIMZUKEKZBLZIMZNZKZUS
      BLZUJDENZKZVBBLUSUTOABCUNUQUPBICUOBPQUMBICULBPQUAUBAURVAUJAURUNENVAAUNUQE
      AUJEKZVCBLOZCLUQERAVDCGSBCIETUDUCAUNDEAUJDKZVEBLOZCLUNDRAVFCHSBCIDTUDUEUF
      UGZAUSVBBFVGUHUI $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d y z F $.  $d x y z $.
    hbfvd2.1 $e |- ( ph -> A. x A. y ph ) $.
    hbfvd2.2 $e |- ( ph -> ( y e. F -> A. x y e. F ) ) $.
    hbfvd2.3 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbfv .  This
       variant of ~ hbfvd allows us to create a closed theorem form by
       replacing the uncommitted antecedent ` ph ` with an appropriate
       substitution instance. $)
    hbfvd2 $p |- ( ph -> ( y e. ( F ` A ) -> A. x y e. ( F ` A ) ) ) $=
      ( vz cv wcel wal cab cfv wi hba1 hbab wceq alrimi syl hbfv 19.21bi abidhb
      a1i fveq1d fveq2d eqtrd eleq2d ax-4 a7s albid 3imtr3d ) ACJZIJZDKZBLZIMZU
      NEKZBLZIMZNZKZVBBLZUMDENZKZVEBLVBVCOABCUQUTUSBICURBPQUPBICUOBPQUAUDAVAVDU
      MAVAUQENVDAUQUTEAUMEKZVFBLOZCLUTERAVGCAACLZBFUBZGSBCIEUCTUEAUQDEAUMDKZVJB
      LOZCLUQDRAVKCVIHSBCIDUCTUFUGUHZAVBVEBAVHBLABLZFAVMCBVMCUIUJTVLUKUL $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d y z F $.  $d x y z $.
    $( Move class substitution in and out of a function value. $)
    csbfv12g $p |- ( A e. C ->
                 [_ A / x ]_ ( F ` B ) = ( [_ A / x ]_ F ` [_ A / x ]_ B ) ) $=
      ( vy vz wcel cv cfv csb hbcsb1g wceq hbcsb1 csbeq1a fveq1d fveq2d 3eqtr3d
      ax-17 csbcog hbfvd wex a9e vex hbfv exlimi ax-mp a1i 3eqtrd csbiegf eqtrd
      hbeq ) BDHZFBAFIZCEJZKZKFBAUNCKZKZFBAUNEKZKZJZABUOKABCKZABEKZJZFGBUPVADUM
      FGURUTUMFSFGBUSDGIZBHFSZLFGBUQDVFLUAUNBMZUPUQUSJZUQUTJVAUPVHMZVGAIUNMZAUB
      VIAFUCVJVIAAGGUPVHAGUNUOFUDZVEUNHASZNAGUQUSAGUNEVKVLNAGUNCVKVLNUEULVJUOCU
      SJUPVHVJCEUSAUNEOPAUNUOOVJCUQUSAUNCOQRUFUGUHVGUQUSUTFBUSOPVGUQURUTFBUQOQU
      IUJAFBUODTUMVAURVCJVDUMURUTVCAFBEDTPUMURVBVCAFBCDTQUKR $.
      $( [11-Nov-2005] $)
  $}

  ${
    $d A y $.  $d F y $.  $d B y $.  $d C y $.  $d x y $.
    $( Move class substitution in and out of a function value.(This is
       ~ csbfv12g with a shortened proof, shortened by Alan Sare,
       10-Nov-2012.)  The proof is derived from the virtual deduction proof
       ~ csbfv12gALTVD .  Although the proof is shorter, the total number of
       steps of all theorems used in the proof is probably longer. $)
    csbfv12gALT $p |- ( A e. C -> [_ A / x ]_ ( F ` B ) =
                ( [_ A / x ]_ F ` [_ A / x ]_ B ) ) $=
      ( vy wcel cfv csb csn cima wceq cab cuni df-fv csbeq2i csbunig wsbc eqtrd
      cv csbabg sbceqg csbima12g csbsng imaeq2d csbconstgfv bitrd abbidv unieqd
      eqeq12d 3eqtrd syl6eqr ) BDGZABCEHZIZABEIZABCIZJZKZFTJZLZFMZNZUQUPHUMUOAB
      ECJZKZUTLZFMZNZIABVGIZNVCABUNVHDFCEOPABVGDQUMVIVBUMVIVFABRZFMVBVFAFBDUAUM
      VJVAFUMVJABVEIZABUTIZLVAABVEUTDUBUMVKUSVLUTUMVKUPABVDIZKUSABVDDEUCUMVMURU
      PABCDUDUESABUTDUFUJUGUHSUIUKFUQUPOUL $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d y A $.  $d y C $.  $d x y F $.
    $( Move class substitution in and out of a function value. $)
    csbfv2g $p |- ( A e. C -> [_ A / x ]_ ( F ` B ) =
                  ( F ` [_ A / x ]_ B ) ) $=
      ( vy wcel cfv csb csbfv12g cv ax-17 csbconstgf fveq1d eqtrd ) BDGZABCEHIA
      BCIZABEIZHQEHABCDEJPQREAFBEDFKEGALMNO $.
      $( [10-Nov-2005] $)

    $( Substitution for a function value. $)
    csbfvg $p |- ( A e. C -> [_ A / x ]_ ( F ` x ) = ( F ` A ) ) $=
      ( wcel cv cfv csb csbfv2g csbvarg fveq2d eqtrd ) BCEZABAFZDGHABNHZDGBDGAB
      NCDIMOBDABCJKL $.
      $( [1-Jan-2006] $)
  $}

  ${
    $d x A $.  $d x F $.
    $( The value of a class exists.  Corollary 6.13 of [TakeutiZaring]
       p. 27. $)
    fvex $p |- ( F ` A ) e. _V $=
      ( vx cfv csn cima wceq cab cuni cvv df-fv wmo wcel moeq unieq vex syl6req
      cv unisn ax-mp immoi moabex uniex eqeltri ) ABDBAEFZCRZEZGZCHZIJCABKUIUHC
      LZUIJMUFUEIZGZCLUJCUKNUHULCUHUKUGIUFUEUGOUFCPSQUATUHCUBTUCUD $.
      $( [30-Dec-1996] $)
  $}

  $( Move a conditional outside of a function.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  fvif $p |- ( F ` if ( ph , A , B ) ) = if ( ph , ( F ` A ) , ( F ` B ) ) $=
    ( cif cfv fveq2 ifsb ) ABCABCEZDFBDFCDFIBDGICDGH $.
    $( [2-Sep-2009] $)

  ${
    $d x y z F $.  $d x y z A $.
    fv3.1 $e |- A e. _V $.
    $( Alternate definition of the value of a function.  Definition 6.11 of
       [TakeutiZaring] p. 26. $)
    fv3 $p |- ( F ` A ) = { x | ( E. y ( x e. y /\ A F y ) /\
              E! y A F y ) } $=
      ( vz cv wcel wbr wa wex weu cfv wceq wb wal elfv wi breq2 sylib bi2 alimi
      ceqsalv anim2i eximi eleq2 anbi12d cbvexv 19.40 simprd df-eu sylibr hbeu1
      vex jca ax-17 hba1 hban hbex hbim bi1 ax-14 syl6 com23 imp3a anc2ri com12
      a4s eximdv syl5bi exlimi imp impbii bitri abbi2i ) AGZBGZHZCVQDIZJZBKZVSB
      LZJZACDMZVPWDHVPFGZHZVSVQWENZOZBPZJZFKZWCFBVPCDEQWKWCWKWAWBWKWFCWEDIZJZFK
      WAWJWMFWIWLWFWIWGVSRZBPWLWHWNBVSWGUAUBVSWLBWEFUNVQWECDSUCTUDUEWMVTFBWEVQN
      WFVRWLVSWEVQVPUFWEVQCDSUGUHTWKWIFKZWBWKWFFKWOWFWIFUIUJVSBFUKZULUOWAWBWKVT
      WBWKRBWBWKBVSBUMWJBFWFWIBWFBUPWHBUQURUSUTWBWOVTWKWPVTWIWJFWIVTWJWIVTWFWHV
      TWFRBWHVRVSWFWHVSVRWFWHVSWGVRWFRVSWGVABFAVBVCVDVEVHVFVGVIVJVKVLVMVNVO $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d x F $.  $d x A $.  $d x B $.
    $( The value of a restricted function. $)
    fvres $p |- ( A e. B -> ( ( F |` B ) ` A ) = ( F ` A ) ) $=
      ( vx wcel cres csn cima cv wceq cab cuni cfv crn wss snssi resabs1 df-ima
      3eqtr4g df-fv rneq 3syl eqeq1d abbidv unieqd ) ABEZCBFZAGZHZDIGZJZDKZLCUH
      HZUJJZDKZLAUGMACMUFULUOUFUKUNDUFUIUMUJUFUGUHFZNZCUHFZNZUIUMUFUHBOUPURJUQU
      SJABPCUHBQUPURUAUBUGUHRCUHRSUCUDUEDAUGTDACTS $.
      $( [16-Feb-2004] $) $( [2-Aug-1994] $)
  $}

  $( The value of a member of the domain of a subclass of a function. $)
  funssfv $p |- ( ( Fun F /\ G C_ F /\ A e. dom G ) ->
                ( F ` A ) = ( G ` A ) ) $=
    ( wfun wss cdm wcel cfv wceq wa cres fvres eqcomd funssres fveq1d sylan9eqr
    3impa ) BDZCBEZACFZGZABHZACHZIUARSJZUBABTKZHZUCUAUFUBATBLMUDAUECBCNOPQ $.
    $( [29-May-2007] $) $( [15-Aug-1994] $)

  ${
    $d y z F $.  $d y z A $.
    tz6.12.1 $e |- A e. _V $.
    $( Function value.  Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12-1 $p |- ( ( A F y /\ E! y A F y ) -> ( F ` A ) = y ) $=
      ( vz cv wbr weu wa cfv wi wex fv3 abeq2i exancom anbi1i ancom bitri sylbi
      wcel eupick com12 adantr 19.8a anim1i anasss sylibr expcom impbid eqrdv )
      BAFZCGZULAHZIZEBCJZUKUNEFZUOTZUPUKTZULUQURKUMUQULURUQURULIZALZUMIZULURKZV
      AEUOEABCDMNZVAUMULURIALZIZVBVAVDUMIVEUTVDUMURULAOPVDUMQRULURAUASSUBUCURUN
      UQURUNIVAUQURULUMVAUSUTUMUSAUDUEUFVCUGUHUIUJ $.
      $( [30-Apr-2004] $)

    $( Function value.  Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12 $p |- ( ( <. A , y >. e. F /\ E! y <. A , y >. e. F ) ->
                 ( F ` A ) = y ) $=
      ( cv cop wcel wbr weu cfv wceq df-br eubii tz6.12-1 syl2anbr ) BAEZFCGZBP
      CHZRAIBCJPKQAIBPCLZRQASMABCDNO $.
      $( [10-Jul-1994] $)
  $}

  ${
    $d x y z w $.  $d z w v F $.
    tz6.12f.1 $e |- ( w e. F -> A. y w e. F ) $.
    $( Function value, using bound-variable hypotheses instead of distinct
       variable conditions. $)
    tz6.12f $p |- ( ( <. x , y >. e. F /\ E! y <. x , y >. e. F ) ->
                 ( F ` x ) = y ) $=
      ( vz cv cop wcel weu wa cfv wceq wi ax-17 opeq2 eleq1d wb hbel cbveu a1i
      anbi12d eqeq2 imbi12d vex tz6.12 chvar ) AGZFGZHZDIZUKFJZKZUHDLZUIMZNUHBG
      ZHZDIZURBJZKZUNUPMZNZFBVBFOUIUPMZUMUTUOVAVCUKURULUSVCUJUQDUIUPUHPQZULUSRV
      CUKURFBBCCUJDCGUJIBOESURFOVDTUAUBUIUPUNUCUDFUHDAUEUFUG $.
      $( [30-Aug-1999] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.
    $( Function value when ` F ` is not a function.  Theorem 6.12(2) of
       [TakeutiZaring] p. 27. $)
    tz6.12-2 $p |- ( -. E! y A F y -> ( F ` A ) = (/) ) $=
      ( vx vz cvv wcel cv wbr weu wn cfv c0 wceq wi wss cab fveq2 wa wal eubidv
      breq1 abbidv sseq12d wex vex simpr ss2abi eqsstri vtoclg ax-17 biidd elab
      fv3 eq0 notbii albii bitr2i sylib sseq2d syl5ib ss0 syl6com fvprc pm2.61i
      a1d ) BFGZBAHZCIZAJZKZBCLZMNZOVKVGVLMPZVMVGVLVJDQZPZVKVNEHZCLZVQVHCIZAJZD
      QZPVPEBFVQBNZVRVLWAVOVQBCRWBVTVJDWBVSVIAVQBVHCUBUAUCUDVRDHZVHGVSSAUEZVTSZ
      DQWADAVQCEUFZUNWEVTDWDVTUGUHUIUJVKVOMVLVKVKETZVOMNZVKEUKWHVQVOGZKZETWGEVO
      UOWJVKEWIVJVJVJDVQWFWCVQNVJULUMUPUQURUSUTVAVLVBVCVGKVMVKBCVDVFVE $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d y F $.  $d y A $.
    tz6.12c.1 $e |- A e. _V $.
    $( Corollary of Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12c $p |- ( E! y A F y -> ( ( F ` A ) = y <-> A F y ) ) $=
      ( cv wbr weu cfv wceq wex euex wi hbeu1 ax-17 hbim tz6.12-1 breq2 biimprd
      expcom syli com12 exlimi mpcom syl5ibcom impbid ) BAEZCFZAGZBCHZUFIZUGUHB
      UICFZUJUGUGAJUHUKUGAKUGUHUKLAUHUKAUGAMUKANOUHUGUKUGUHUJUKUGUHUJABCDPSZUJU
      KUGUIUFBCQZRTUAUBUCUMUDULUE $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.
    tz6.12i.1 $e |- A e. _V $. $( (Note: hypothesis could be eliminated) $)
    $( Corollary of Theorem 6.12(2) of [TakeutiZaring] p. 27. $)
    tz6.12i $p |- ( B =/= (/) -> ( ( F ` A ) = B -> A F B ) ) $=
      ( vy cfv wceq c0 wne wbr cvv wcel wi fvex eleq1 mpbii eqeq2 neeq1 imbi12d
      cv breq2 weu wb tz6.12-2 necon1ai tz6.12c syl biimpd syl6bir vtoclg mpcom
      pm2.43a com12 ) ACFZBGZBHIZABCJZBKLZUOUPUQMZUOUNKLURACNUNBKOPUNETZGZUTHIZ
      AUTCJZMZMUOUSMEBKUTBGZVAUOVDUSUTBUNQVEVBUPVCUQUTBHRUTBACUASSVBVAVCVAVBUNH
      IZVAVCMUNUTHRVFVAVCVFVCEUBZVAVCUCVGUNHEACUDUEEACDUFUGUHUIULUJUKUM $.
      $( [6-Apr-2007] $) $( [30-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( The value of a class outside its domain is the empty set. $)
    ndmfv $p |- ( -. A e. dom F -> ( F ` A ) = (/) ) $=
      ( vy vx cvv wcel cdm wn cfv c0 wceq wi cv wbr weu euex eleq1 breq1 exbidv
      wex vex eldm vtoclbg syl5ibr con3d tz6.12-2 syl6 fvprc a1d pm2.61i ) AEFZ
      ABGZFZHZABIJKZLUKUNACMZBNZCOZHUOUKURUMURUMUKUQCTZUQCPDMZULFUTUPBNZCTUMUSD
      AEUTAULQUTAKVAUQCUTAUPBRSCUTBDUAUBUCUDUECABUFUGUKHUOUNABUHUIUJ $.
      $( [24-Aug-1995] $)
  $}

  ${
    ndmfvrcl.1 $e |- dom F = S $.
    ndmfvrcl.2 $e |- -. (/) e. S $.
    $( Reverse closure law for function with the empty set not in its
       domain. $)
    ndmfvrcl $p |- ( ( F ` A ) e. S -> A e. S ) $=
      ( cfv wcel cdm wn c0 ndmfv eleq1d mtbiri con4i syl6eleq ) ACFZBGZACHZBARG
      ZQSIZQJBGETPJBACKLMNDO $.
      $( [26-Apr-1996] $)
  $}

  $( If a function value has a member, the argument belongs to the domain. $)
  elfvdm $p |- ( A e. ( F ` B ) -> B e. dom F ) $=
    ( cfv wcel c0 wne cdm ne0i ndmfv necon1ai syl ) ABCDZEMFGBCHEZMAINMFBCJKL
    $.
    $( [12-Feb-2007] $)

  $( The value of a non-member of a restriction is the empty set. $)
  nfvres $p |- ( -. A e. B -> ( ( F |` B ) ` A ) = (/) ) $=
    ( wcel wn cres cdm cfv c0 wceq cin wa dmres eleq2i elin bitri simplbi con3i
    ndmfv syl ) ABDZEACBFZGZDZEAUBHIJUDUAUDUAACGZDZUDABUEKZDUAUFLUCUGACBMNABUEO
    PQRAUBST $.
    $( [13-Nov-1995] $)

  ${
    $d x y A $.  $d x y F $.
    $( If the restriction of a class to a singleton is not a function, its
       value is the empty set.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nfunsn $p |- ( -. Fun ( F |` { A } ) -> ( F ` A ) = (/) ) $=
      ( vx vy cfv c0 wceq csn cres wfun wn wrel cv wbr wmo wal weu eumo sylbi
      wa wcel vex brres wb elsn breq1 biimpac immoi syl tz6.12-2 alrimiv relres
      nsyl4 jctil dffun6 sylibr con1i ) ABEFGZBAHZIZJZURKZUTLZCMZDMZUTNZDOZCPZT
      VAVBVHVCVBVGCAVEBNZDQZVGURVJVIDOVGVIDRVFVIDVFVDVEBNZVDUSUAZTVIVDVEBUSDUBU
      CVLVKVIVLVDAGVKVIUDCAUEVDAVEBUFSUGSUHUIDABUJUMUKBUSULUNCDUTUOUPUQ $.
      $( [22-Oct-2011] $) $( [8-Aug-2010] $)
  $}

  $( Equal values imply equal values in a restriction. $)
  fveqres $p |- ( ( F ` A ) = ( G ` A ) ->
                ( ( F |` B ) ` A ) = ( ( G |` B ) ` A ) ) $=
    ( wcel cfv wceq cres wi fvres eqeq12d biimprd wn nfvres eqtr4d a1d pm2.61i
    c0 ) ABEZACFZADFZGZACBHFZADBHFZGZISUEUBSUCTUDUAABCJABDJKLSMZUEUBUFUCRUDABCN
    ABDNOPQ $.
    $( [13-Nov-1995] $)

  ${
    $d x y A $.  $d x y F $.  $d x y B $.
    funbrfv.1 $e |- B e. _V $.
    $( The second argument of a binary relation on a function is the function's
       value. $)
    funbrfv $p |- ( Fun F -> ( A F B -> ( F ` A ) = B ) ) $=
      ( vx vy wfun wbr cfv wceq cvv wcel wa funrel brrelex wi cv anbi2d imbi12d
      wrel sylan breq1 fveq2 eqeq1d breq2 eqeq2 weu vex tz6.12-1 sylan2 anabss7
      funeu vtocl2g mpan2 mpcom ex ) CGZABCHZACIZBJZAKLZUQURMZUTUQCTURVACNABCOU
      AVABKLVBUTPZDUQEQZFQZCHZMZVDCIZVEJZPUQAVECHZMZUSVEJZPVCEFABKKVDAJZVGVKVIV
      LVMVFVJUQVDAVECUBRVMVHUSVEVDACUCUDSVEBJZVKVBVLUTVNVJURUQVEBACUERVEBUSUFSU
      QVFVIVGVFVFFUGVIEFCULFVDCEUHUIUJUKUMUNUOUP $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y F $.  $d x y B $.
    funopfv.1 $e |- B e. _V $.
    $( The second element in an ordered pair member of a function is the
       function's value. $)
    funopfv $p |- ( Fun F -> ( <. A , B >. e. F -> ( F ` A ) = B ) ) $=
      ( cop wcel wbr wfun cfv wceq df-br funbrfv syl5bir ) ABECFABCGCHACIBJABCK
      ABCDLM $.
      $( [19-Jul-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( The second element in an ordered pair member of a function is the
       function's value. $)
    funopfvg $p |- ( ( B e. C /\ Fun F ) ->
                   ( <. A , B >. e. F -> ( F ` A ) = B ) ) $=
      ( vx wcel wfun cop cfv wceq wi cv opeq2 eleq1d imbi12d imbi2d vex funopfv
      eqeq2 vtoclg imp ) BCFDGZABHZDFZADIZBJZKZUBAELZHZDFZUEUHJZKZKUBUGKEBCUHBJ
      ZULUGUBUMUJUDUKUFUMUIUCDUHBAMNUHBUESOPAUHDEQRTUA $.
      $( [14-Sep-1999] $)
  $}

  ${
    $d x y F $.  $d x A $.  $d x y B $.  $d x C $.
    $( Equivalence of function value and binary relation. $)
    fnbrfvbg $p |- ( ( F Fn A /\ B e. A /\ C e. D ) ->
                   ( ( F ` B ) = C <-> B F C ) ) $=
      ( vx vy wcel wfn cfv wceq wbr wb wa cv wi eqeq2 bibi12d weu vtoclg imbi2d
      breq2 fneu breq1 eubidv fveq2 eqeq1d imbi12d vex tz6.12c adantl mpd 3coml
      3impib ) CDHZEAIZBAHZBEJZCKZBCELZMZUOUPUQVAUPUQNZURFOZKZBVCELZMZPVBVAPFCD
      VCCKZVFVAVBVGVDUSVEUTVCCURQVCCBEUBRUAVBVEFSZVFFABEUCUQVHVFPZUPGOZVCELZFSZ
      VJEJZVCKZVKMZPVIGBAVJBKZVLVHVOVFVPVKVEFVJBVCEUDZUEVPVNVDVKVEVPVMURVCVJBEU
      FUGVQRUHFVJEGUIUJTUKULTUNUM $.
      $( [17-Feb-2013] $)
  $}

  ${
    fnfvbr.1 $e |- C e. _V $.
    $( Equivalence of function value and binary relation. $)
    fnbrfvb $p |- ( ( F Fn A /\ B e. A ) ->
                   ( ( F ` B ) = C <-> B F C ) ) $=
      ( wfn wcel cvv cfv wceq wbr wb fnbrfvbg mp3an3 ) DAFBAGCHGBDICJBCDKLEABCH
      DMN $.
      $( [19-Apr-2004] $)

    $( Equivalence of function value and ordered pair membership. $)
    fnopfvb $p |- ( ( F Fn A /\ B e. A ) ->
                   ( ( F ` B ) = C <-> <. B , C >. e. F ) ) $=
      ( wfn wcel wa cfv wceq wbr cop fnbrfvb df-br syl6bb ) DAFBAGHBDICJBCDKBCL
      DGABCDEMBCDNO $.
      $( [7-Nov-1995] $)
  $}

  ${
    funbrfvb.1 $e |- B e. _V $.
    $( Equivalence of function value and binary relation. $)
    funbrfvb $p |- ( ( Fun F /\ A e. dom F ) ->
                   ( ( F ` A ) = B <-> A F B ) ) $=
      ( wfun cdm wfn wcel cfv wceq wbr wb df-fn eqid mpbiran2 fnbrfvb sylanbr )
      CEZCCFZGZASHACIBJABCKLTRSSJCSMSNOSABCDPQ $.
      $( [26-Mar-2006] $)

    $( Equivalence of function value and ordered pair membership.  Theorem
       4.3(ii) of [Monk1] p. 42. $)
    funopfvb $p |- ( ( Fun F /\ A e. dom F ) ->
                   ( ( F ` A ) = B <-> <. A , B >. e. F ) ) $=
      ( wfun cdm wcel wa cfv wceq wbr cop funbrfvb df-br syl6bb ) CEACFGHACIBJA
      BCKABLCGABCDMABCNO $.
      $( [26-Jan-1997] $)
  $}

  ${
    $d x y z w A $.  $d x y B $.  $d x y z w F $.  $d x y C $.
    $( Function value in terms of a binary relation. $)
    funbrfvbg $p |- ( ( Fun F /\ A e. dom F /\ B e. C ) ->
                   ( ( F ` A ) = B <-> A F B ) ) $=
      ( vx wcel wfun cdm cfv wceq wbr wb wa cv eqeq2 breq2 bibi12d imbi2d vex
      wi funbrfvb vtoclg 3impib 3coml ) BCFZDGZADHFZADIZBJZABDKZLZUEUFUGUKUFUGM
      ZUHENZJZAUMDKZLZTULUKTEBCUMBJZUPUKULUQUNUIUOUJUMBUHOUMBADPQRAUMDESUAUBUCU
      D $.
      $( [20-Jan-2007] $)
    $( Function value in terms of a binary relation.  (Contributed by Mario
       Carneiro, 19-Mar-2014.) $)
    funbrfv2bg $p |- ( ( Fun F /\ B e. C ) ->
                       ( A F B <-> ( A e. dom F /\ ( F ` A ) = B ) ) ) $=
      ( wfun wcel wa wbr cdm cfv wceq wi wrel funrel releldm ex adantr pm4.71rd
      syl wb funbrfvbg 3expa an32s pm5.32da bitr4d ) DEZBCFZGZABDHZADIFZUIGUJAD
      JBKZGUHUIUJUFUIUJLZUGUFDMZULDNUMUIUJABDOPSQRUHUJUKUIUFUJUGUKUITZUFUJUGUNA
      BCDUAUBUCUDUE $.
      $( [19-Mar-2014] $)

    $( Representation of a function in terms of its values. $)
    dffn5 $p |- ( F Fn A <->
                   F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) } ) $=
      ( vz vw wfn cv wcel cfv wceq wa copab cop wb wal fnop ex vex wrel fnopfvb
      pm4.71rd eqcom syl5bb pm5.32da bitr4d eleq1 fveq2 eqeq2d anbi12d opelopab
      eqeq1 anbi2d syl6bbr fnrel relopab eqrel sylancl mpbird fvex eqid fnopab2
      alrimivv fneq1 mpbiri impbii ) DCGZDAHZCIZBHZVHDJZKZLZABMZKZVGVOEHZFHZNZD
      IZVRVNIZOZFPEPZVGWAEFVGVSVPCIZVQVPDJZKZLZVTVGVSWCVSLWFVGVSWCVGVSWCCVPVQDQ
      RUBVGWCWEVSWEWDVQKVGWCLVSVQWDUCCVPVQDFSZUAUDUEUFVMWCVJWDKZLWFABVPVQESWGVH
      VPKZVIWCVLWHVHVPCUGWIVKWDVJVHVPDUHUIUJVJVQKWHWEWCVJVQWDULUMUKUNVCVGDTVNTV
      OWBOCDUOVMABUPEFDVNUQURUSVOVGVNCGABCVKVNVHDUTVNVAVBCDVNVDVEVF $.
      $( [29-Jan-2004] $)

    $( The range of a function expressed as a collection of the function's
       values. $)
    fnrnfv $p |- ( F Fn A -> ran F = { y | E. x e. A y = ( F ` x ) } ) $=
      ( wfn crn cv cop wcel wex cab cfv wceq wrex dfrn3 wa cdm vex opeldm fndm
      eleq2d syl5ib pm4.71rd eqcom fnopfvb syl5rbbr bitrd exbidv df-rex syl6bbr
      pm5.32da abbidv syl5eq ) DCEZDFAGZBGZHDIZAJZBKUPUODLZMZACNZBKABDOUNURVABU
      NURUOCIZUTPZAJVAUNUQVCAUNUQVBUQPVCUNUQVBUQUODQZIUNVBUOUPDARSUNVDCUOCDTUAU
      BUCUNVBUQUTUTUSUPMUNVBPUQUSUPUDCUOUPDBRUEUFUKUGUHUTACUIUJULUM $.
      $( [20-Oct-2005] $)

    $( A member of a function's range is a value of the function. $)
    fvelrnb $p |- ( F Fn A -> ( B e. ran F <-> E. x e. A ( F ` x ) = B ) ) $=
      ( vy wfn crn wcel cv cfv wceq wrex cab fnrnfv eleq2d cvv fvex eleq1 mpbii
      syl6bb rexlimivw eqeq1 eqcom rexbidv elab3 ) DBFZCDGZHCEIZAIZDJZKZABLZEMZ
      HUJCKZABLZUFUGUMCAEBDNOULUOECUNCPHZABUNUJPHUPUIDQUJCPRSUAUHCKZUKUNABUQUKC
      UJKUNUHCUJUBCUJUCTUDUET $.
      $( [31-Oct-1995] $)

    $( Alternate definition of the image of a function.  (Contributed by Raph
       Levien, 20-Nov-2006.) $)
    dfimafn $p |- ( ( Fun F /\ A C_ dom F ) ->
                  ( F " A ) = { y | E. x e. A ( F ` x ) = y } ) $=
      ( wfun cdm wss wa cv cfv wceq wrex cab wbr cima wcel wb ssel vex funbrfvb
      ex syl9r imp31 rexbidva abbidv dfima2 syl6reqr ) DEZCDFZGZHZAIZDJBIZKZACL
      ZBMULUMDNZACLZBMDCOUKUOUQBUKUNUPACUHUJULCPZUNUPQZUJURULUIPZUHUSCUIULRUHUT
      USULUMDBSTUAUBUCUDUEABDCUFUG $.
      $( [20-Nov-2006] $)

    $( Alternate definition of the image of a function as an indexed union of
       singletons of function values.  (Contributed by Raph Levien,
       20-Nov-2006.) $)
    dfimafn2 $p |- ( ( Fun F /\ A C_ dom F ) ->
                   ( F " A ) = U_ x e. A { ( F ` x ) } ) $=
      ( vy wfun cdm wss wa cima cv cfv wceq cab ciun wrex dfimafn iunab syl6eqr
      csn wcel df-sn eqcom abbii eqtri a1i iuneq2i ) CEBCFGHZCBIZABAJZCKZDJZLZD
      MZNZABUJSZNUGUHULABODMUNADBCPULADBQRABUOUMUOUMLUIBTUOUKUJLZDMUMDUJUAUPULD
      UKUJUBUCUDUEUFR $.
      $( [20-Nov-2006] $)

    $( Membership relation for the values of a function whose image is a
       subclass.  (Contributed by Raph Levien, 20-Nov-2006.) $)
    funimass4 $p |- ( ( Fun F /\ A C_ dom F ) ->
                    ( ( F " A ) C_ B <-> A. x e. A ( F ` x ) e. B ) ) $=
      ( vy cdm wss wfun cima cv cfv wcel wral wb wi wal wa wceq wrex syl5bb wbr
      dfss2 eqcom ssel vex funbrfvb ex syl9 imp31 rexbidva elima imbi1d r19.23v
      syl6rbbr syl6bbr albidv ralcom4 eleq1 ceqsalv ralbii bitr3i syl6bb ancoms
      fvex ) BDFZGZDHZDBIZCGZAJZDKZCLZABMZNVIEJZVHLZVNCLZOZEPZVFVGQZVMEVHCUBVSV
      RVNVKRZVPOZABMZEPZVMVSVQWBEVSVQVTABSZVPOWBVSVOWDVPVSWDVJVNDUAZABSVOVSVTWE
      ABVTVKVNRZVSVJBLZQWEVNVKUCVFVGWGWFWENZVFWGVJVELZVGWHBVEVJUDVGWIWHVJVNDEUE
      ZUFUGUHUITUJAVNDBWJUKUNULVTVPABUMUOUPWCWAEPZABMVMWAAEBUQWKVLABVPVLEVKVJDV
      DVNVKCURUSUTVAVBTVC $.
      $( [20-Nov-2006] $)

    $( Function value in an image.  Part of Theorem 4.4(iii) of [Monk1] p. 42.
       (The proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    fvelima $p |- ( ( Fun F /\ A e. ( F " B ) ) ->
                  E. x e. B ( F ` x ) = A ) $=
      ( wfun cima wcel wa cv cfv wceq wrex simpr wbr elimag ibi wi cop funopfvg
      df-br syl5bi ancoms reximdv syl5 mpd ) DEZBDCFZGZHZUHAIZDJBKZACLZUFUHMUHU
      JBDNZACLZUIULUHUNABDCUGOPUIUMUKACUHUFUMUKQUMUJBRDGUHUFHUKUJBDTUJBUGDSUAUB
      UCUDUE $.
      $( [22-Oct-2011] $) $( [29-Apr-2004] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d x y C $.  $d x y F $.
    $( Function value in an image.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.)  (An unnecessary distinct variable restriction was removed
       by David Abernethy, 17-Dec-2011.) $)
    fvelimab $p |- ( ( F Fn A /\ B C_ A ) -> ( C e. ( F " B ) <->
                  E. x e. B ( F ` x ) = C ) ) $=
      ( vy wfn wss wa cima wcel cv cfv wceq wrex cvv anim2i eleq1 wb wi rexbidv
      elex fvex mpbii rexlimivw eqeq2 bibi12d imbi2d wfun cdm fnfun adantr fndm
      cab sseq2d biimpar dfimafn syl2anc abeq2d vtoclg impcom pm5.21nd ) EBGZCB
      HZIZDECJZKZALZEMZDNZACOZVEDPKZIVGVLVEDVFUBQVKVLVEVJVLACVJVIPKVLVHEUCVIDPR
      UDUEQVLVEVGVKSZVEFLZVFKZVIVNNZACOZSZTVEVMTFDPVNDNZVRVMVEVSVOVGVQVKVNDVFRV
      SVPVJACVNDVIUFUAUGUHVEVQFVFVEEUIZCEUJZHZVFVQFUNNVCVTVDBEUKULVCWBVDVCWABCB
      EUMUOUPAFCEUQURUSUTVAVB $.
      $( [25-Dec-2011] $) $( [20-Jan-2007] $)
  $}

  ${
    $d x B $.  $d x C $.  $d x F $.  $d x V $.
    $( Membership in the preimage of a singleton, under a function.
       (Contributed by Mario Carneiro, 12-May-2014.) $)
    fniniseg $p |- ( ( F Fn A /\ B e. V ) ->
                 ( C e. ( `' F " { B } ) <-> ( C e. A /\ ( F ` C ) = B ) ) ) $=
      ( vx wfn wcel wa cvv ccnv csn cima wceq wi elex a1i adantr wb wbr cfv cdm
      cv eleq1 breq1 bibi12d imbi2d vex eliniseg vtoclg adantll wfun funbrfv2bg
      impcom fnfun sylan fndm ad2antrr eleq2d anbi1d 3bitrd ex pm5.21ndd ) DAGZ
      BEHZIZCJHZCDKBLMZHZCAHZCDUABNZIZVIVGOVFCVHPQVLVGOVFVJVGVKCAPRQVFVGVIVLSVF
      VGIZVICBDTZCDUBZHZVKIZVLVEVGVIVNSZVDVGVEVRVEFUCZVHHZVSBDTZSZOVEVROFCJVSCN
      ZWBVRVEWCVTVIWAVNVSCVHUDVSCBDUEUFUGDBVSEFUHUIUJUNUKVFVNVQSZVGVDDULVEWDADU
      OCBEDUMUPRVMVPVJVKVMVOACVDVOANVEVGADUQURUSUTVAVBVC $.
      $( [12-May-2014] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.
    $( The indexed intersection of a function's values is the intersection of
       its range. $)
    fniinfv $p |- ( F Fn A -> |^|_ x e. A ( F ` x ) = |^| ran F ) $=
      ( vy wfn crn cint cv cfv wceq wrex cab ciin fnrnfv inteqd dfiin2 syl6reqr
      fvex ) CBEZCFZGDHAHZCIZJABKDLZGABUBMSTUCADBCNOADBUBUACRPQ $.
      $( [20-Oct-2005] $)

    $( Singleton of function value. $)
    fnsnfv $p |- ( ( F Fn A /\ B e. A ) -> { ( F ` B ) } = ( F " { B } ) ) $=
      ( vy wfn wcel wa cv cfv wceq cab wbr csn cima eqcom fnbrfvb syl5bb abbidv
      vex df-sn a1i wrel fnrel relimasn syl adantr 3eqtr4d ) CAEZBAFZGZDHZBCIZJ
      ZDKZBUKCLZDKZULMZCBMNZUJUMUODUMULUKJUJUOUKULOABUKCDSPQRUQUNJUJDULTUAUHURU
      PJZUIUHCUBUSACUCDBCUDUEUFUG $.
      $( [22-May-1998] $)
  $}

  $( The image of a pair under a funtion.  (Contributed by Jeff Madsen,
     6-Jan-2011.) $)
  fnimapr $p |- ( ( F Fn A /\ B e. A /\ C e. A ) ->
                          ( F " { B , C } ) = { ( F ` B ) , ( F ` C ) } ) $=
    ( wfn wcel w3a csn cima cun cfv fnsnfv 3adant3 3adant2 uneq12d eqcomd df-pr
    cpr wceq imaeq2i imaundi eqtri 3eqtr4g ) DAEZBAFZCAFZGZDBHZIZDCHZIZJZBDKZHZ
    CDKZHZJZDBCRZIZUMUORUGUQULUGUNUIUPUKUDUEUNUISUFABDLMUDUFUPUKSUEACDLNOPUSDUH
    UJJZIULURUTDBCQTDUHUJUAUBUMUOQUC $.
    $( [6-Jan-2011] $)

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z F $.
    ssimaex.1 $e |- A e. _V $.
    $( The existence of a subimage. $)
    ssimaex $p |- ( ( Fun F /\ B C_ ( F " A ) ) ->
                 E. x ( x C_ A /\ B = ( F " x ) ) ) $=
      ( vy vz vw cima wss cdm cv wceq wa wex cfv wcel wi ex adantr wfun imaeq2i
      cin cres dmres imadmres eqtr3i sseq2i ssrab2 ssel2 adantll fvelima eleq1a
      crab wrex anim2d fveq2 eleq1d elrab syl6ibr simpr jcad reximdv2 adantl wb
      a1i wfn funfn inss2 sstri fvelimab mpan2 sylbi sylibrd syld adantlr eleq1
      mpd biimpcd rexlimiv impbid eqrdv inex1 rabex sseq1 imaeq2 eqeq2d anbi12d
      syl6 cla4ev sylancr inss1 sstr anim1i eximi syl sylan2br ) CDBIZJDUAZCDBD
      KZUCZIZJZALZBJZCDXDIZMZNZAOZXBWRCDDBUDKZIXBWRXJXADDBUEUBDBUFUGUHWSXCNZXDX
      AJZXGNZAOZXIXKFLZDPZCQZFXAUNZXAJZCDXRIZMZXNXQFXAUIZXKGCXTXKGLZCQZYCXTQZXK
      YDYEXKYDNYCXBQZYEXCYDYFWSCXBYCUJUKWSYDYFYERXCWSYDNZYFHLZDPZYCMZHXAUOZYEWS
      YFYKRYDWSYFYKHYCXADULSTYGYKYJHXRUOZYEYDYKYLRWSYDYJYJHXAXRYDYHXAQZYJNZYHXR
      QZYJYDYNYMYICQZNZYOYDYJYPYMYCCYIUMUPXQYPFYHXAXOYHMXPYICXOYHDUQURUSZUTYNYJ
      RYDYMYJVAVFVBVCVDWSYEYLVEZYDWSDWTVGZYSDVHYTXRWTJYSXRXAWTYBBWTVIVJHWTXRYCD
      VKVLVMTVNVOVPVRSWSYEYDRXCWSYEYLYDWSYEYLHYCXRDULSYJYDHXRYOYQYJYDRZYRYPUUAY
      MYJYPYDYIYCCVQVSVDVMVTWITWAWBXMXSYANAXRXQFXABWTEWCWDXDXRMZXLXSXGYAXDXRXAW
      EUUBXFXTCXDXRDWFWGWHWJWKXMXHAXLXEXGXLXABJXEBWTWLXDXABWMVLWNWOWPWQ $.
      $( [8-Apr-2007] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.
    $( The existence of a subimage.  (Contributed by FL, 15-Apr-2007.) $)
    ssimaexg $p |- ( ( A e. C /\ Fun F /\ B C_ ( F " A ) ) ->
                 E. x ( x C_ A /\ B = ( F " x ) ) ) $=
      ( vy wcel wfun cima wss cv wceq wa wex imaeq2 sseq2d anbi2d sseq2 anbi1d
      wi exbidv imbi12d vex ssimaex vtoclg 3impib ) BDGEHZCEBIZJZAKZBJZCEUJILZM
      ZANZUGCEFKZIZJZMZUJUOJZULMZANZTUGUIMZUNTFBDUOBLZURVBVAUNVCUQUIUGVCUPUHCUO
      BEOPQVCUTUMAVCUSUKULUOBUJRSUAUBAUOCEFUCUDUEUF $.
      $( [10-Apr-2007] $)
  $}

  $( A simplified expression for the value of a function when we know it's a
     function. $)
  funfv $p |- ( Fun F -> ( F ` A ) = U. ( F " { A } ) ) $=
    ( wfun cdm wcel cfv csn cima cuni wceq fvex unisn wfn df-fn mpbiran2 fnsnfv
    wa eqid unieqd c0 sylanbr syl5eqr ex wn ndmfv ndmima syl6eq eqtr4d pm2.61d1
    uni0 ) BCZABDZEZABFZBAGHZIZJZUKUMUQUKUMQZUNUNGZIUPUNABKLURUSUOUKBULMZUMUSUO
    JUTUKULULJBULNULROULABPUASUBUCUMUDZUNTUPABUEVAUPTITVAUOTABUFSUJUGUHUI $.
    $( [22-May-1998] $)

  ${
    $d y A $.  $d y F $.
    $( The value of a function.  Definition of function value in [Enderton]
       p. 43. $)
    funfv2 $p |- ( Fun F -> ( F ` A ) = U. { y | A F y } ) $=
      ( wfun cfv csn cima cuni cv wbr cab funfv wrel funrel relimasn syl unieqd
      wceq eqtrd ) CDZBCECBFGZHBAICJAKZHBCLTUAUBTCMUAUBRCNABCOPQS $.
      $( [11-May-2005] $) $( [22-May-1998] $)
  $}

  ${
    $d w z A $.  $d w z F $.  $d w y z $.
    funfv2f.1 $e |- ( z e. A -> A. y z e. A ) $.
    funfv2f.2 $e |- ( z e. F -> A. y z e. F ) $.
    $( The value of a function.  Version of ~ funfv2 using a bound-variable
       hypotheses instead of distinct variable conditions. $)
    funfv2f $p |- ( Fun F -> ( F ` A ) = U. { y | A F y } ) $=
      ( vw wfun cfv cv wbr cab cuni funfv2 wcel ax-17 hbbr breq2 cbvab unieqi
      syl6eq ) DHCDICGJZDKZGLZMCAJZDKZALZMGCDNUDUGUCUFGAABCUBDEFBJUBOAPQUFGPUBU
      ECDRSTUA $.
      $( [19-Feb-2006] $)
  $}

  $( Value of the union of two functions when the domains are separate.
     (Contributed by FL, 7-Nov-2011.) $)
  fvun $p |- ( ( ( Fun F /\ Fun G ) /\ ( dom F i^i dom G ) = (/) ) ->
                ( ( F u. G ) ` A ) = ( ( F ` A ) u. ( G ` A ) ) ) $=
    ( wfun wa cdm cin c0 wceq cun cfv csn cima cuni funun funfv imaundir eqcomd
    syl a1i unieqd uniun anim12i adantr uneq12 syl5eq 3eqtrd ) BDZCDZEZBFCFGHIZ
    EZABCJZKZUMALZMZNZBUOMZCUOMZJZNZABKZACKZJZULUMDUNUQIBCOAUMPSULUPUTUPUTIULBC
    UOQTUAULVAURNZUSNZJZVDURUSUBULVEVBIZVFVCIZEZVGVDIUJVJUKUHVHUIVIUHVBVEABPRUI
    VCVFACPRUCUDVEVBVFVCUESUFUG $.
    $( [7-Nov-2011] $)

  ${
    $d x y z A $.  $d x y z F $.
    $( Alternate definition of function value ~ df-fv that doesn't require
       dummy variables. $)
    dffv2 $p |- ( F ` A ) = U. ( ( F " { A } )
            \ U. U. ( ( ( F |` { A } ) o. `' ( F |` { A } ) ) \ _I ) ) $=
      ( vy vz vx cid cdif cuni wceq cvv wcel wn c0 wss unieqd syl6eq wa wbr wex
      wal csn cres wfun cfv cima ccnv ccom snidb fvres sylbi fvprc eqtr4d funfv
      pm2.61i wrel df-fun simprbi ssdif0 sylib uni0 resima dif0 eqtr4i syl6reqr
      difeq2d eqtrd syl5eqr nfunsn cv wi dffun3 relres mpbiran iman albii alnex
      bitri exbii exnal 3bitrri con1bii eximi snssi residm dmeqi ssdmres biimpi
      cdm ax-4 syl breldm eleq2 elsn syl6bb biimpa syl2an breq1d biimpd pm2.43d
      vex anim1d eximdv exlimdv mpan9 eleq2i elimasni sylbir cpr cop uniop opex
      ex unisn wrex brrelexi brcnvg sylancr biimpar breq2 breq1 anbi12d rcla4ev
      wb adantl mpancom ancoms syldan an32s eldif rexv brco df-br 3bitr2ri ideq
      anim1i equcom 3bitr3i notbii uniss syl5eqssr anbi12i bitr2i sylibr simpld
      3syl prss syl5 exlimiv ssrdv ndmima difeq1d 0dif pm2.61d1 ) BAUAZUBZUCZAB
      UDZBUUNUEZUUOUUOUFZUGZFGZHZHZGZHZIUUPUUQAUUOUDZUVEAJKZUVFUUQIZUVGAUUNKUVH
      AUHAUUNBUIUJUVGLUVFMUUQAUUOUKABUKULUNUUPUVFUUOUUNUEZHUVEAUUOUMUUPUVIUVDUU
      PUVDUURMGZUVIUUPUVCMUURUUPUVCMHZMUUPUVBMUUPUVBUVKMUUPUVAMUUPUUTFNZUVAMIUU
      PUUOUOZUVLUUOUPUQUUTFURUSOUTPOUTPVEUVIUURUVJBUUNVAZUURVBVCVDOVFVGUUPLZUUQ
      MUVEABVHUVOUVEUVKMUVOUVDMUVOAUUOWHZKZUVDMIZUVOUVQUVRUVOUVQQZUURUVCNUVRUVS
      CUURUVCUVSADVIZUUORZUVTCVIZIZLZQZDSZUWBUURKZUWBUVCKZVJZUVOEVIZUVTUUORZUWD
      QZDSZESZUVQUWFUVOUWMCTZESZUWNUWPUUPUUPUWKUWCVJZDTZCSZETZUWOLZETUWPLUUPUVM
      UWTEDCUUOVKBUUNVLZVMUWSUXAEUWSUWMLZCSUXAUWRUXCCUWRUWLLZDTUXCUWQUXDDUWKUWC
      VNVOUWLDVPVQVRUWMCVSVQVOUWOEVPVTWAUWOUWMEUWMCWIWBUJUVQUWMUWFEUVQUWLUWEDUV
      QUWKUWAUWDUVQUWKUWAUVQUWKUWKUWAVJUVQUWKQZUWKUWAUXEUWJAUVTUUOUVQUVPUUNIZUW
      JUVPKZUWJAIZUWKUVQUUNUVPNZUXFAUVPWCUXIUVPUUOUUNUBZWHZUUNUXJUUOBUUNWDWEUXI
      UXKUUNIUUNUUOWFWGVGWJUWJUVTUUOEWTWKUXFUXGUXHUXFUXGUWJUUNKUXHUVPUUNUWJWLEA
      WMWNWOWPWQWRXLWSXAXBXCXDUWEUWIDUWGAUWBUUORZUWEUWHUWGUWBUVIKUXLUVIUURUWBUV
      NXEUUOAUWBXFXGUWEUXLUWHUWEUXLQZUWHUVTUVCKZUXMUWBUVTXHZUVCNUWHUXNQUXMUXOUW
      BUVTXIZHZUVCUWBUVTXJUXMUXPUVBNUXQUVCNUXMUXPUXPUAZHZUVBUXPUWBUVTXKXMUXMUXP
      UVAKZUXRUVANUXSUVBNUXMUWBUWJUUSRZUWKQZEJXNZUWDQZUXTUWAUXLUWDUYDUWAUXLQUYC
      UWDUWAUXLUWBAUUSRZUYCUWAUYEUXLUWAUWBJKUVGUYEUXLYCCWTZAUVTUUOUXBXOZUWBAJJU
      UOXPXQXRUYEUWAUYCUVGUYEUWAQZUYCUWAUVGUYEUYGYDUYBUYHEAJUXHUYAUYEUWKUWAUWJA
      UWBUUSXSUWJAUVTUUOXTYAYBYEYFYGYOYHUXTUXPUUTKZUXPFKZLZQUYDUXPUUTFYIUYIUYCU
      YKUWDUYCUYBESUWBUVTUUTRUYIUYBEYJEUWBUVTUUOUUSUYFDWTZYKUWBUVTUUTYLYMUYJUWC
      UWBUVTFRUWBUVTIUYJUWCUWBUVTUYLYNUWBUVTFYLCDYPYQYRUUAUUBUSUXPUVAWCUXRUVAYS
      UUEYTUXPUVBYSWJYTUWBUVTUVCUYFUYLUUFUUCUUDXLUUGUUHWJUUIUURUVCURUSXLUVQLZUV
      DMUVCGMUYMUURMUVCUYMUURUVIMUVNAUUOUUJVGUUKUVCUULPUUMOUTPULUN $.
      $( [4-Aug-2010] $)
  $}

  ${
    $d y z A $.  $d y z F $.  $d y z G $.
    $( Domains of a function composition. $)
    dmfco $p |- ( ( Fun G /\ A e. dom G ) ->
               ( A e. dom ( F o. G ) <-> ( G ` A ) e. dom F ) ) $=
      ( vz vy wfun cdm wcel wa ccom cv cop wex cfv cvv vex exbidv wceq syl5bb
      wb eldm2g opelco2g mpan2 bitrd adantl eldm2 opeq1 eleq1d ceqsexv funopfvb
      fvex eqcom anbi1d syl5bbr bitr4d ) CFZACGZHZIZABCJZGHZADKZLCHZVBEKZLZBHZI
      ZDMZEMZACNZBGHZURVAVITUPURVAAVDLUTHZEMVIEAUTUQUAURVLVHEURVDOHVLVHTEPDAVDB
      CUQOUBUCQUDUEVKVJVDLZBHZEMUSVIEVJBACUKZUFUSVNVHEVNVBVJRZVFIZDMUSVHVFVNDVJ
      VOVPVEVMBVBVJVDUGUHUIUSVQVGDUSVPVCVFVPVJVBRUSVCVBVJULAVBCDPUJSUMQUNQSUO
      $.
      $( [27-Jan-1997] $)

    $( Value of a function composition.  Similar to Exercise 5 of
       [TakeutiZaring] p. 28. $)
    fvco $p |- ( ( Fun F /\ Fun G /\ A e. dom G ) ->
               ( ( F o. G ) ` A ) = ( F ` ( G ` A ) ) ) $=
      ( vz wfun cdm wcel cfv wceq wa cop wi fvex funopfvb wex wb cvv adantl ex
      c0 w3a ccom dmfco anbi2d eqid mpbii cv opelco2g mpan2 opeq1 ceqsexv eqcom
      eleq1d vex syl5bb anbi1d exbidv syl5bbr bitr4d syl5ibr sylbid exp4b com3r
      3imp1 funco sylan 3adantl3 mpbird ndmfv notbid syl6bi imp 3adant1 pm2.61d
      wn eqtr4d ) BEZCEZACFZGZUAZABCUBZFGZAWBHZACHZBHZIZWAWCWGWAWCJWGAWFKWBGZVQ
      VRVTWCWHVRVTVQWCWHLVRVTVQWCWHVRVTJZVQWCJVQWEBFGZJZWHWIWCWJVQABCUCZUDWKWHW
      IWEWFKZBGZWKWFWFIWNWFUEWEWFBWEBMZNUFWIWHADUGZKCGZWPWFKZBGZJZDOZWNVTWHXAPZ
      VRVTWFQGXBWODAWFBCVSQUHUIRWNWPWEIZWSJZDOWIXAWSWNDWEACMXCWRWMBWPWEWFUJUMUK
      WIXDWTDWIXCWQWSXCWEWPIWIWQWPWEULAWPCDUNNUOUPUQURUSUTVAVBVCVDVQVRWCWGWHPZV
      TVQVRJWBEWCXEBCVEAWFWBWONVFVGVHSVRVTWCVOZWGLVQWIXFWGWIXFJWDTWFXFWDTIWIAWB
      VIRWIXFWFTIZWIXFWJVOXGWIWCWJWLVJWEBVIVKVLVPSVMVN $.
      $( [22-Apr-2006] $)
  $}

  $( Value of a function composition.  Similar to second part of Theorem 3H of
     [Enderton] p. 47.  (The proof was shortened by Andrew Salmon,
     22-Oct-2011.) $)
  fvco2 $p |- ( ( Fun F /\ G Fn A /\ C e. A ) ->
             ( ( F o. G ) ` C ) = ( F ` ( G ` C ) ) ) $=
    ( wfun wfn wcel ccom cfv wceq wa wi cdm fvco 3expib com12 funfni 3impib ) C
    EZDAFZBAGZBCDHIBDICIJZTUAKSUBSUBLABDSDEZBDMGZKUBSUCUDUBBCDNOPQPR $.
    $( [22-Oct-2011] $) $( [9-Oct-2004] $)

  $( Value of a function composition. $)
  fvco3 $p |- ( ( Fun F /\ G : A --> B /\ C e. A ) ->
             ( ( F o. G ) ` C ) = ( F ` ( G ` C ) ) ) $=
    ( wf wfun wfn wcel ccom cfv wceq ffn fvco2 syl3an2 ) ABEFDGEAHCAICDEJKCEKDK
    LABEMACDENO $.
    $( [21-Aug-2006] $) $( [3-Jan-2004] $)

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    $( Closed theorem form of ~ fvopab4 . $)
    fvopab4t $p |- ( ( A. x A. y ( x = A -> B = C )
      /\ A. x F = { <. x , y >. | ( x e. D /\ y = B ) }
      /\ ( A e. D /\ C e. V ) ) -> ( F ` A ) = C ) $=
      ( wcel wa cv wceq wi wal copab cvv cfv elex wb wfun anim2i eleq1 bi2anan9
      w3a cop eqeq2 a2i 2alimi 3ad2ant1 eqeq1 anbi2d gen2 a1i opelopabt syl3anc
      simp3 simp3l eqidd mpbir2and simp2 eleqtrrd simp3r funopab4 funeqd mpbiri
      ex 19.21bi funopfvg syl2anc mpd syl3an3 ) CFIZEHIZJAKZCLZDELZMZBNANZGVNFI
      ZBKZDLZJZABOZLZANZVLEPIZJZCGQELZVMWFVLEHRUAVRWEWGUDZCEUEZGIZWHWIWJWCGWIWJ
      WCIZVLEELZWIVOWBVLVTELZJZSZMZBNANZWNWOVLWMJZSMZBNANZWGWLWSSVRWEWRWGVQWQAB
      VOVPWPVOVPWPVOVSVLVPWAWNVNCFUBDEVTUFUCVFUGUHUIXAWIWTABWNWNWMVLVTEEUJUKULU
      MVRWEWGUPWBWOWSABCEFPUNUOVRWEVLWFUQWIEURUSWIWDAVRWEWGUTVGZVAWIWFGTZWKWHMV
      RWEVLWFVBWIXCWCTVSABDVCWIGWCXBVDVECEPGVHVIVJVK $.
      $( [21-Feb-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ch $.
    fvopab3.1 $e |- B e. _V $.
    fvopab3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    fvopab3.3 $e |- ( y = B -> ( ps <-> ch ) ) $.
    fvopab3.4 $e |- ( x e. C -> E! y ph ) $.
    fvopab3.5 $e |- F = { <. x , y >. | ( x e. C /\ ph ) } $.
    $( Value of a function given by ordered-pair class abstraction. $)
    fvopab3 $p |- ( A e. C -> ( ( F ` A ) = B <-> ch ) ) $=
      ( wcel cv wa wceq cvv wb cop copab cfv eleq1 anbi12d anbi2d opelopabg wfn
      mpan2 fnopab fnopfvb mpan eleq2i syl6bb ibar 3bitr4d ) FHOZFGUAZDPZHOZAQZ
      DEUBZOZUQCQZFIUCGRZCUQGSOVCVDTJVAUQBQVDDEFGHSUSFRUTUQABUSFHUDKUEEPGRBCUQL
      UFUGUIUQVEURIOZVCIHUHUQVEVFTADEHIMNUJHFGIJUKULIVBURNUMUNUQCUOUP $.
      $( [6-Mar-1996] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ch $.
    fvopab3ig.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    fvopab3ig.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    fvopab3ig.3 $e |- ( x e. C -> E* y ph ) $.
    fvopab3ig.4 $e |- F = { <. x , y >. | ( x e. C /\ ph ) } $.
    $( Value of a function given by ordered-pair class abstraction. $)
    fvopab3ig $p |- ( ( A e. C /\ B e. D ) -> ( ch -> ( F ` A ) = B ) ) $=
      ( wcel wa cv cfv wceq wi copab cop eleq1 anbi12d anbi2d opelopabg biimpar
      exp43 pm2.43a imp wfun funopab moanimv mpbir mpgbir funopfvg mpan2 adantl
      wmo syld fveq1i eqeq1i syl6ibr ) FHOZGIOZPZCFDQZHOZAPZDEUAZRZGSZFJRZGSVFC
      FGUBVJOZVLVDVECVNTZVEVDVOVDVEVDCVNVFVNVDCPZVIVDBPVPDEFGHIVGFSVHVDABVGFHUC
      KUDEQGSBCVDLUEUFUGUHUIUJVEVNVLTZVDVEVJUKZVQVRVIEUSZDVIDEULVSVHAEUSTMVHAEU
      MUNUOFGIVJUPUQURUTVMVKGFJVJNVAVBVC $.
      $( [23-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    fvopab4g.1 $e |- ( x = A -> B = C ) $.
    fvopab4g.2 $e |- F = { <. x , y >. | ( x e. D /\ y = B ) } $.
    $( Value of a function given by ordered-pair class abstraction. $)
    fvopab4g $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( wcel wa wceq cfv eqid cv eqeq2d eqeq1 wmo moeq a1i fvopab3ig mpi ) CFKE
      GKLEEMZCHNEMEOBPZDMZUEEMUDABCEFGHAPZCMDEUEIQUEEERUFBSUGFKBDTUAJUBUC $.
      $( [23-Oct-1999] $)

    ${
      fvopab4.3 $e |- C e. _V $.
      $( Value of a function given by ordered-pair class abstraction. $)
      fvopab4 $p |- ( A e. D -> ( F ` A ) = C ) $=
        ( wcel cvv cfv wceq fvopab4g mpan2 ) CFKELKCGMENJABCDEFLGHIOP $.
        $( [23-Oct-1999] $)
    $}
  $}

  ${
    $d v w z A $.  $d u v w y B $.  $d z w v C $.  $d v w x y D $.  $d u x z $.
    fvopab4gf.1 $e |- ( z e. A -> A. x z e. A ) $.
    fvopab4gf.2 $e |- ( z e. C -> A. x z e. C ) $.
    fvopab4gf.3 $e |- ( x = A -> B = C ) $.
    fvopab4gf.4 $e |- F = { <. x , y >. | ( x e. D /\ y = B ) } $.
    $( Value of a function given by an ordered-pair class abstraction.  This
       version of ~ fvopab4g uses bound-variable hypotheses instead of distinct
       variable conditions. $)
    fvopab4gf $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( vw vv wcel wa cv wceq ax-17 cfv csb copab fveq1i vex hbcsb1 hbeleq hban
      wb eleq1 adantr csbeq1a eqeqan12rd anbi12d cbvopab eqtri csbhypf fvopab4g
      id eqid syl5eq ) DGPFHPQDIUAZDNRZGPZORZAVCEUBZSZQZNOUCZUAZFVBDARZGPZBRZES
      ZQZABUCZUAVJDIVPMUDDVPVIVOVHABNOVONTVOOTVDVGAVDATAOVFAOVCENUEVEVCPATUFUGU
      HVHBTVKVCSZVMVESZQVLVDVNVGVQVLVDUIVRVKVCGUJUKVRVQVMVEEVFVRUSAVCEULUMUNUOU
      DUPNODVFFGHVIANCDEFJKLUQVIUTURVA $.
      $( [20-Dec-2008] $) $( [8-Nov-2005] $)
  $}

  ${
    $d z A $.  $d y B $.  $d z B $.  $d x y C $.  $d x z $.
    fvopab4sf.1 $e |- A e. _V $.
    fvopab4sf.2 $e |- B e. _V $.
    fvopab4sf.3 $e |- ( z e. A -> A. x z e. A ) $.
    fvopab4sf.4 $e |- F = { <. x , y >. | ( x e. C /\ y = B ) } $.
    $( Value of a function given by ordered-pair class abstraction, using
       explicit class substitution.  TODO:  Eliminate redundant first
       hypothesis. $)
    fvopab4sf $p |- ( A e. C -> ( F ` A ) = [_ A / x ]_ B ) $=
      ( wcel csb cvv cfv wceq csbex hbcsb1 csbeq1a fvopab4gf mpan2 ) DFLADEMZNL
      DGOUBPADEHIQABCDEUBFNGJACDEHJRADESKTUA $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d w A $.  $d w y z B $.  $d w x y z C $.  $d w F $.
    fvopab4s.1 $e |- B e. _V $.
    fvopab4s.2 $e |- F = { <. x , y >. | ( x e. C /\ y = B ) } $.
    $( Value of a function given by ordered-pair class abstraction, using
       explicit class substitution. $)
    fvopab4s $p |- ( A e. C -> ( F ` A ) = [_ A / x ]_ B ) $=
      ( vw vz cv cfv csb wceq fveq2 csbeq1 eqeq12d vex wcel ax-17 fvopab4sf
      vtoclga ) IKZFLZAUCDMZNCFLZACDMZNICEUCCNUDUFUEUGUCCFOAUCCDPQABJUCDEFIRGJK
      UCSATHUAUB $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x y A $.
    fvopab4ndm.1 $e |- F = { <. x , y >. | ( x e. A /\ ph ) } $.
    $( Value of a function given by an ordered-pair class abstraction, outside
       of its domain. $)
    fvopab4ndm $p |- ( -. B e. A -> ( F ` B ) = (/) ) $=
      ( wcel wn cdm cfv c0 wceq cv wa copab dmeqi dmopabss eqsstri sseli con3i
      ndmfv syl ) EDHZIEFJZHZIEFKLMUFUDUEDEUEBNDHAOBCPZJDFUGGQABCDRSTUAEFUBUC
      $.
      $( [28-Mar-2008] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d x y z w C $.
    fvopabg.1 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered-pair class abstraction. $)
    fvopabg $p |- ( ( A e. V /\ C e. W ) ->
                    ( { <. x , y >. | y = B } ` A ) = C ) $=
      ( wcel cvv cv wceq copab cfv elex wa vex biantrur opabbii fvopab4g sylan
      ) CFICJIEGICBKDLZABMZNELCFOABCDEJGUCHUBAKJIZUBPABUDUBAQRSTUA $.
      $( [2-Sep-2003] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.
    fvopabn.1 $e |- ( x = A -> B = C ) $.
    $( This somewhat non-intuitive theorem tells us the value of its function
       is the empty set when the class ` C ` it would otherwise map to is a
       proper class.  This is a technical lemma that can help eliminate
       redundant sethood antecedents otherwise required by ~ fvopabg . $)
    fvopabn $p |- ( -. C e. _V -> ( { <. x , y >. | y = B } ` A ) = (/) ) $=
      ( vz vw cvv wcel wn cv wceq c0 wa cuni csn cab wex vex copab cfv wi df-fv
      cima wne snnz df-ne mpbi cop dfima3 elsn anbi1i exbii opeq1 eleq1d syl5bb
      ceqsexgv wb eqeq2d eqeq1 opelopabg mpan2 bitrd abbidv eleq1 mpbii exlimiv
      con3i abn0 necon1bbii sylib syl5eq eqeq1d eqcom syl6bb mtbiri unieqd uni0
      sylan9eq nexdv syl6eq ex fvprc a1d pm2.61i ) CIJZEIJZKZCBLZDMZABUAZUBZNMZ
      UCWGWIWNWGWIOZWMNPZNWOWMWLCQZUEZGLZQZMZGRZPWPGCWLUDWOXBNWOXAGSZKXBNMWOXAG
      WOXAWTNMZWTNUFXDKWSGTUGWTNUHUIWOXANWTMXDWOWRNWTWOWRWSWQJZWSHLZUJZWLJZOZGS
      ZHRZNGHWLWQUKWGWIXKXFEMZHRZNWGXJXLHWGXJCXFUJZWLJZXLXJWSCMZXHOZGSWGXOXIXQG
      XEXPXHGCULUMUNXHXOGCIXPXGXNWLWSCXFUOUPURUQWGXFIJZXOXLUSHTZWKWJEMXLABCXFII
      ALCMDEWJFUTWJXFEVAVBVCVDVEWIXLHSZKXMNMXTWHXLWHHXLXRWHXSXFEIVFVGVHVIXTXMNX
      LHVJVKVLVTVMVNNWTVOVPVQWAXCXBNXAGVJVKVLVRVMVSWBWCWGKWNWICWLWDWEWF $.
      $( [21-Oct-2003] $)
  $}

  ${
    $d z w v A $.  $d y w v B $.  $d z w v C $.  $d x y w v $.  $d x z $.
    fvopabgf.1 $e |- ( z e. A -> A. x z e. A ) $.
    fvopabgf.2 $e |- ( z e. C -> A. x z e. C ) $.
    fvopabgf.3 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered-pair class abstraction. $)
    fvopabgf $p |- ( ( A e. D /\ C e. R ) ->
                    ( { <. x , y >. | y = B } ` A ) = C ) $=
      ( vv vw wcel wa cv wceq copab cfv ax-17 csb vex hbcsb1 csbeq1a eqeqan12rd
      hbeleq id cbvopab fveq1i csbhypf fvopabg syl5eq ) DGNFHNODBPZEQZABRZSDLPZ
      AMPZEUAZQZMLRZSFDUOUTUNUSABMLUNMTUNLTALURALUQEMUBUPUQNATUCUFUSBTUMUPQZAPU
      QQUMUPEURVAUGAUQEUDUEUHUIMLDURFGHAMCDEFIJKUJUKUL $.
      $( [21-Oct-2003] $)

    $( The value of a function given by an ordered-pair class abstraction is
       the empty set when the class it would otherwise map to is a proper
       class.  This version of ~ fvopabn uses bound-variable hypotheses instead
       of distinct variable conditions. $)
    fvopabnf $p |- ( -. C e. _V -> ( { <. x , y >. | y = B } ` A ) = (/) ) $=
      ( vv vw cvv wcel wn cv wceq copab cfv csb ax-17 vex hbcsb1 hbeleq csbeq1a
      c0 id eqeqan12rd cbvopab fveq1i csbhypf fvopabn syl5eq ) FLMNDBOZEPZABQZR
      DJOZAKOZESZPZKJQZRUEDUOUTUNUSABKJUNKTUNJTAJURAJUQEKUAUPUQMATUBUCUSBTUMUPP
      ZAOUQPUMUPEURVAUFAUQEUDUGUHUIKJDURFAKCDEFGHIUJUKUL $.
      $( [21-Dec-2008] $) $( [21-Oct-2003] $)
  $}

  ${
    $d z A $.  $d y B $.  $d z C $.  $d x y $.  $d x u $.  $d x z $.
    fvopabf.1 $e |- ( z e. A -> A. x z e. A ) $.
    fvopabf.2 $e |- ( z e. C -> A. x z e. C ) $.
    fvopabf.3 $e |- A e. _V $.
    fvopabf.4 $e |- C e. _V $.
    fvopabf.5 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered-pair class abstraction. $)
    fvopabf $p |- ( { <. x , y >. | y = B } ` A ) = C $=
      ( cvv wcel cv wceq copab cfv fvopabgf mp2an ) DLMFLMDBNEOABPQFOIJABCDEFLL
      GHKRS $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d x z A $.  $d y B $.  $d x z C $.  $d x y z $.
    fvopab.1 $e |- A e. _V $.
    fvopab.2 $e |- C e. _V $.
    fvopab.3 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by an ordered-pair class abstraction. $)
    fvopab $p |- ( { <. x , y >. | y = B } ` A ) = C $=
      ( vz cv wcel ax-17 fvopabf ) ABICDEIJZCKALNEKALFGHM $.
      $( [17-Apr-1995] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    $( Value of a function given by an ordered-pair class abstraction. $)
    fvopab2 $p |- ( ( x e. A /\ B e. C ) ->
                   ( { <. x , y >. | ( x e. A /\ y = B ) } ` x ) = B ) $=
      ( vz wcel cv wceq wa copab cfv wex wi elisset ax-17 hbopab2 hbfv cvv weu
      hbeq hbim vex eleq1 mpbii euanv opabid eubii eueq anbi2i 3bitr4ri tz6.12f
      cop sylanbr sylan2b exp43 pm2.43a mpdi com12 eqeq2 sylibd exlimi impcom
      syl ) DEGZAHZCGZVFVGBHZDIZJZABKZLZDIZVEVIBMVGVMNZBDEOVIVNBVGVMBVGBPBFFVLD
      BFVFVKVJABFQZFHZVFGBPRVPDGBPUAUBVIVGVLVHIZVMVGVIVQVGVIDSGZVQVIVHSGVRBUCVH
      DSUDUEVIVGVRVQNVGVIVGVRVQVGVRJZVJVFVHUMVKGZBTZVQVJBTVGVIBTZJWAVSVGVIBUFVT
      VJBVJABUGZUHVRWBVGBDUIUJUKVJVTWAVQWCABFVKVOULUNUOUPUQURUSVHDVLUTVAVBVDVC
      $.
      $( [2-Feb-2004] $) $( [30-Aug-1999] $)
  $}

  ${
    $d x z A $.  $d y z B $.  $d x y $.
    fvopabs.1 $e |- A e. _V $.
    fvopabs.2 $e |- B e. _V $.
    $( The value of a function given by an ordered-pair class abstraction,
       using class substitution. $)
    fvopabs $p |- ( { <. x , y >. | y = B } ` A ) = [_ A / x ]_ B $=
      ( vz csb cv wcel ax-17 hbcsb1 csbex csbeq1a fvopabf ) ABGCDACDHGICJAKZAGC
      DEPLEACDEFMACDNO $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d z F $.  $d z ph $.  $d x ps $.
    fvopab5.1 $e |- F = { <. x , y >. | ph } $.
    fvopab5.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( The value of a function that is expressed as an ordered pair
       abstraction. $)
    fvopab5 $p |- ( ( Fun F /\ A e. B ) -> ( F ` A ) = U. { y | ps } ) $=
      ( vz wfun wcel cfv cv cab cuni ax-17 wceq cop syl5bb copab hbopab2 eleq2i
      wbr hbxfreq funfv2f elisset df-br bitri wb hbopab1 hbel hbbi opeq1 eleq1d
      wex opabid bitr3d exlimi abbidv syl unieqd sylan9eq ) GKEFLZEGMEDNZGUDZDO
      ZPBDOZPDJEGJNZELDQDJGACDUAZHACDJUBUEUFVDVGVHVDCNZERZCUPZVGVHRCEFUGVMVFBDV
      FEVESZVJLZVMBVFVNGLVOEVEGUHGVJVNHUCUIVLVOBUJCVOBCCJJVNVJVIVNLCQACDJUKULBC
      QUMVLVKVESZVJLZVOBVLVPVNVJVKEVEUNUOVQAVLBACDUQITURUSTUTVAVBVC $.
      $( [19-Feb-2006] $)
  $}

  ${
    $d A x y $.  $d ps x y $.  $d B y $.  $d C x y $.
    fvopab6.1 $e |- F = { <. x , y >. | ( ph /\ y = B ) } $.
    fvopab6.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    fvopab6.3 $e |- ( x = A -> B = C ) $.
    $( Value of a function given by ordered-pair class abstraction.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    fvopab6 $p |- ( ( A e. D /\ C e. R /\ ps ) -> ( F ` A ) = C ) $=
      ( wcel cv wceq wa cab cuni wfun w3a cfv wmo copab wal funeq ax-mp funopab
      bitri moeq moani mpgbir simp1 anbi12d fvopab5 sylancr simpr pm3.2 impbid2
      wb eqeq2d abbidv unieqd csn eqcomi unieqi unisng syl5eq sylan9eqr 3adant1
      df-sn eqtrd ) EHNZGINZBUAZEJUBZBDOZGPZQZDRZSZGVOJTZVMVPWAPWBAVQFPZQZDUCZC
      WBWDCDUDZTZWECUEJWFPWBWGUTKJWFUFUGWDCDUHUIWCADDFUJUKULVMVNBUMWDVSCDEHJKCO
      EPZABWCVRLWHFGVQMVAUNUOUPVNBWAGPVMBVNWAVRDRZSZGBVTWIBVSVRDBVSVRBVRUQBVRUR
      USVBVCVNWJGVDZSGWIWKWKWIDGVKVEVFGIVGVHVIVJVL $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.  $d x y G $.
    $( Equality of functions is determined by their values.  Exercise 4 of
       [TakeutiZaring] p. 28. $)
    eqfnfv $p |- ( ( F Fn A /\ G Fn B ) -> ( F = G <->
                 ( A = B /\ A. x e. A ( F ` x ) = ( G ` x ) ) ) ) $=
      ( vy wfn wa wceq cv cfv cdm wi fndm syl5ib syl2an wcel wb wal wn wral a1d
      dmeq eqeq12 fveq1 ralrimiv a1i jcad fneq2 biimparc cop df-ral vex fnopfvb
      eqeq1 adantlr adantll bibi12d ex a2d com3r opeldm eleq2d anim12ii syl6com
      con3d pm5.21 pm2.61i alrimdv alimdv syl5bi wrel fnrel eqrel sylibrd exp32
      sylan2 imp4b impbid ) DBGZECGZHZDEIZBCIZAJZDKZWEEKZIZABUAZHWBWCWDWIVTDLZB
      IZELZCIZWCWDMWABDNZCENWCWJWLIWKWMHWDDEUCWJBWLCUDOPWCWIMWBWCWHABWCWHWEBQZW
      EDEUEUBUFUGUHVTWAWDWIWCVTWAWDWIWCMZWAWDHVTEBGZWPWDWQWABCEUIUJVTWQHZWIWEFJ
      ZUKZDQZWTEQZRZFSZASZWCWIWOWHMZASWRXEWHABULWRXFXDAWRXFXCFWOWRXFXCMZMWRXFWO
      XCWRWOWHXCWRWOWHXCMWHWFWSIZWGWSIZRWRWOHZXCWFWGWSUOXJXHXAXIXBVTWOXHXARWQBW
      EWSDFUMZUNUPWQWOXIXBRVTBWEWSEXKUNUQUROUSUTVAWRWOTZXATZXBTZHZXGVTXLXMWQXNV
      TXAWOXAWEWJQVTWOWEWSDAUMZVBVTWJBWEWNVCOVFWQXBWOXBWEWLQWQWOWEWSEXPVBWQWLBW
      EBENVCOVFVDXOXCXFXAXBVGUBVEVHVIVJVKVTDVLEVLWCXERWQBDVMBEVMAFDEVNPVOVQVPVR
      VS $.
      $( [5-Feb-2004] $) $( [3-Aug-1994] $)

    $( Equality of functions is determined by their values.  Special case of
       Exercise 4 of [TakeutiZaring] p. 28 (with domain equality omitted).
       (The proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    eqfnfv2 $p |- ( ( F Fn A /\ G Fn A ) -> ( F = G <->
                 A. x e. A ( F ` x ) = ( G ` x ) ) ) $=
      ( wfn wa wceq cv cfv wral eqfnfv eqid biantrur syl6bbr ) CBEDBEFCDGBBGZAH
      ZCIPDIGABJZFQABBCDKOQBLMN $.
      $( [22-Oct-2011] $) $( [3-Aug-1994] $)

    $d x B $.
    $( Derive equality of functions from equality of their values.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    eqfnfv3 $p |- ( ( F Fn A /\ G Fn B ) -> ( F = G <-> ( B C_ A /\ A. x e. A
                                  ( x e. B /\ ( F ` x ) = ( G ` x ) ) ) ) ) $=
      ( wfn wa wceq cv cfv wral wcel eqfnfv eqss ancom bitri anbi1i anass dfss3
      wss r19.26 bitr4i anbi2i 3bitri syl6bb ) DBFECFGDEHBCHZAIZDJUGEJHZABKZGZC
      BTZUGCLZUHGABKZGZABCDEMUJUKBCTZGZUIGUKUOUIGZGUNUFUPUIUFUOUKGUPBCNUOUKOPQU
      KUOUIRUQUMUKUQULABKZUIGUMUOURUIABCSQULUHABUAUBUCUDUE $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x z A $.  $d y z F $.  $d y z G $.  $d x y $.
    eqfnfvf2.1 $e |- ( y e. F -> A. x y e. F ) $.
    eqfnfvf2.2 $e |- ( y e. G -> A. x y e. G ) $.
    $( Equality of functions is determined by their values.  Special case of
       Exercise 4 of [TakeutiZaring] p. 28 (with domain equality omitted).
       This version of ~ eqfnfv2 uses bound-variable hypotheses instead of
       distinct variable conditions. $)
    eqfnfv2f $p |- ( ( F Fn A /\ G Fn A ) -> ( F = G <->
                 A. x e. A ( F ` x ) = ( G ` x ) ) ) $=
      ( vz wfn wa wceq cv cfv wral eqfnfv2 wcel ax-17 hbfv hbeq fveq2 eqeq12d
      cbvral syl6bb ) DCIECIJDEKHLZDMZUDEMZKZHCNALZDMZUHEMZKZACNHCDEOUGUKHACABB
      UEUFABUDDFBLUDPAQZRABUDEGULRSUKHQUDUHKUEUIUFUJUDUHDTUDUHETUAUBUC $.
      $( [22-Mar-2009] $) $( [29-Jan-2004] $)
  $}

  ${
    $d F x $.  $d G x $.
    $( Equality of functions is determined by their values.  (Contributed by
       Scott Fenton, 19-Jun-2011.) $)
    eqfunfv $p |- ( ( Fun F /\ Fun G ) -> ( F = G <->
                    ( dom F = dom G /\
                      A. x e. dom F ( F ` x ) = ( G ` x ) ) ) ) $=
      ( wfun cdm wfn wceq cv cfv wral wa wb funfn eqfnfv syl2anb ) BDBBEZFCCEZF
      BCGPQGAHZBIRCIGAPJKLCDBMCMAPQBCNO $.
      $( [10-Jul-2011] $) $( [19-Jun-2011] $)
  $}

  ${
    $d x B $.  $d x F $.  $d x G $.
    $( Equality of restricted functions is determined by their values. $)
    fvreseq $p |- ( ( ( F Fn A /\ G Fn A ) /\ B C_ A ) ->
         ( ( F |` B ) = ( G |` B ) <-> A. x e. B ( F ` x ) = ( G ` x ) ) ) $=
      ( wfn wa wss cres wceq cv cfv wral fnssres anim12i anandirs eqfnfv2 fvres
      wb wcel eqeq12d ralbiia syl6bb syl ) DBFZEBFZGCBHZGDCIZCFZECIZCFZGZUHUJJZ
      AKZDLZUNELZJZACMZSUEUFUGULUEUGGUIUFUGGUKBCDNBCENOPULUMUNUHLZUNUJLZJZACMUR
      ACUHUJQVAUQACUNCTUSUOUTUPUNCDRUNCERUAUBUCUD $.
      $( [6-Feb-2004] $) $( [3-Aug-1994] $)
  $}

  ${
    $d x y z A $.  $d y B $.  $d z F $.  $d w x z C $.  $d w F $.
    elrnopabg.1 $e |- F = { <. x , y >. | ( x e. A
             /\ y = B ) } $.
    $( Membership in the range of an ordered pair class abstraction. $)
    elrnopabg $p |- ( A. x e. A B e. D ->
                    ( C e. ran F <-> E. x e. A C = B ) ) $=
      ( vz vw wcel wral cvv wceq wrex wb cv cfv wa ax-17 crn elex ralimi biimpi
      wfn weu eueq fnopabg sylib fvelrnb copab hbopab1 hbxfreq hbfv hbeq eqeq1d
      fveq2 cbvrex hbra1 ra4 ancld imp fveq1i fvopab2 syl5eq syl6bb syl rexbida
      eqcom syl5bb sylan9bbr mpdan ) DFKZACLDMKZACLZEGUAKZEDNZACOZPZVMVNACDFUBU
      CVOGCUEZVSVOBQDNZBUFZACLVTVNWBACVNWBBDUGUDUCWAABCGHUHUIVTVPIQZGRZENZICOZV
      OVRICEGUJWFAQZGRZENZACOVOVRWEWIIACAJJWDEAJWCGAJGWGCKZWASZABUKZHWKABJULUMJ
      QZWCKATUNWMEKATUOWIITWCWGNWDWHEWCWGGUQUPURVOWIVQACVNACUSVOWJSWJVNSZWIVQPV
      OWJWNVOWJVNVNACUTVAVBWNWIDENVQWNWHDEWNWHWGWLRDWGGWLHVCABCDMVDVEUPDEVIVFVG
      VHVJVKVLVG $.
      $( [27-Aug-2007] $)
  $}

  ${
    $d x y z A $.  $d y B $.  $d z F $.  $d w x z C $.  $d w F $.
    elrnopab.1 $e |- B e. _V $.
    elrnopab.2 $e |- F = { <. x , y >. | ( x e. A
             /\ y = B ) } $.
    $( Membership in the range of an ordered pair class abstraction. $)
    elrnopab $p |- ( C e. ran F <-> E. x e. A C = B ) $=
      ( cvv wcel crn wceq wrex wb elrnopabg cv a1i mprg ) DIJZEFKJEDLACMNACABCD
      EIFHOSAPCJGQR $.
      $( [30-Aug-2004] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( The range of a choice function (a function that chooses an element from
       each member of its domain) is included in the union of its domain. $)
    chfnrn $p |- ( ( F Fn A /\ A. x e. A ( F ` x ) e. x ) -> ran F C_ U. A ) $=
      ( vy wfn cv cfv wcel wral wa cuni wrex wceq fvelrnb biimpd hbra1 wi eleq1
      crn ra4 biimpcd syl6 reximdai sylan9 eluni2 syl6ibr ssrdv ) CBEZAFZCGZUIH
      ZABIZJZDCSZBKZUMDFZUNHZUPUIHZABLZUPUOHUHUQUJUPMZABLZULUSUHUQVAABUPCNOULUT
      URABUKABPULUIBHUKUTURQUKABTUTUKURUJUPUIRUAUBUCUDAUPBUEUFUG $.
      $( [31-Aug-1999] $)
  $}

  ${
    $d x F $.  $d x A $.
    $( Ordered pair with function value.  Part of Theorem 4.3(i) of [Monk1]
       p. 41. $)
    funfvop $p |- ( ( Fun F /\ A e. dom F ) -> <. A , ( F ` A ) >. e. F ) $=
      ( vx wfun cdm wcel wa cv cfv wceq wex fvex isseti eqcom wi funopfvb opeq2
      cop vex eleq1d biimprcd syl6bi pm2.43d syl5bi exlimdv mpi ) BDABEFGZCHZAB
      IZJZCKAUIRZBFZCUIABLMUGUJULCUJUIUHJZUGULUHUINUGUMULUGUMAUHRZBFZUMULOAUHBC
      SPUMULUOUMUKUNBUIUHAQTUAUBUCUDUEUF $.
      $( [14-Oct-1996] $)
  $}

  $( Two ways to say that ` A ` is in the domain of ` F ` .  (Contributed by
     Mario Carneiro, 1-May-2014.) $)
  funfvbrb $p |- ( Fun F -> ( A e. dom F <-> A F ( F ` A ) ) ) $=
    ( wfun cdm wcel cfv wbr wa funfvop df-br sylibr wrel funrel releldm impbida
    cop sylan ) BCZABDEZAABFZBGZRSHATPBEUAABIATBJKRBLUASBMATBNQO $.
    $( [1-May-2014] $)

  $( A member of a preimage is a function value argument. $)
  fvimacnvi $p |- ( ( Fun F /\ A e. ( `' F " B ) ) -> ( F ` A ) e. B ) $=
    ( wfun ccnv cima wcel cfv csn wss snssi funimass2 sylan2 fvex snss cdm wceq
    wa cnvimass sseli wfn funfn fnsnfv sylanb sseq1d syl5bb mpbird ) CDZACEBFZG
    ZRZACHZBGZCAIZFZBJZUJUHUNUIJUPAUIKUNBCLMUMULIZBJUKUPULBACNOUKUQUOBUJUHACPZG
    ZUQUOQZUIURACBSTUHCURUAUSUTCUBURACUCUDMUEUFUG $.
    $( [4-May-2007] $)

  $( The argument of a function value belongs to the pre-image of any class
     containing the function value.  (Contributed by Raph Levien, 20-Nov-2006.
     He remarks:  "This proof is unsatisfying, because it seems to me that
     ~ funimass2 could probably be strengthened to a biconditional.") $)
  fvimacnv $p |- ( ( Fun F /\ A e. dom F ) ->
                 ( ( F ` A ) e. B <-> A e. ( `' F " B ) ) ) $=
    ( wfun cdm wcel wa cfv ccnv cima csn cop funfvop wb fvex mpan adantl mpbird
    cvv wss opelcnvg elimasng imass2 sylbi sseld syl5com wi fvimacnvi ex adantr
    snss impbid ) CDZACEZFZGZACHZBFZACIZBJZFZUPAUSUQKZJZFZURVAUPVDUQALUSFZUPVEA
    UQLCFZACMUOVEVFNZUMUQSFZUOVGACOZUQASUNCUAPQRUOVDVENZUMVHUOVJVIUSUQASUNUBPQR
    URVCUTAURVBBTVCUTTUQBVIUKVBBUSUCUDUEUFUMVAURUGUOUMVAURABCUHUIUJUL $.
    $( [20-Nov-2006] $)

  ${
    $d F x $.  $d A x $.  $d B x $.
    $( A kind of contraposition law that infers an image subclass from a
       subclass of a pre-image.  (Contributed by Raph Levien, 20-Nov-2006.  He
       remarks:  "Likely this could be proved directly, and ~ fvimacnv would be
       the special case of ` A ` being a singleton, but it works this way round
       too.") $)
    funimass3 $p |- ( ( Fun F /\ A C_ dom F ) ->
                    ( ( F " A ) C_ B <-> A C_ ( `' F " B ) ) ) $=
      ( vx wfun cdm wss wa cima cv ccnv wcel wral funimass4 wb ssel fvimacnv ex
      cfv syl9r imp31 ralbidva bitrd dfss3 syl6bbr ) CEZACFZGZHZCAIBGZDJZCKBIZL
      ZDAMZAULGUIUJUKCSBLZDAMUNDABCNUIUOUMDAUFUHUKALZUOUMOZUHUPUKUGLZUFUQAUGUKP
      UFURUQUKBCQRTUAUBUCDAULUDUE $.
      $( [20-Nov-2006] $)

    $( A subclass of a preimage in terms of function values. $)
    funimass5 $p |- ( ( Fun F /\ A C_ dom F ) ->
                    ( A C_ ( `' F " B ) <-> A. x e. A ( F ` x ) e. B ) ) $=
      ( wfun cdm wss wa cima ccnv cv cfv wcel wral funimass3 funimass4 bitr3d )
      DEBDFGHDBICGBDJCIGAKDLCMABNBCDOABCDPQ $.
      $( [15-May-2007] $)

    $( Two ways of specifying that a function is constant on a subdomain. $)
    funconstss $p |- ( ( Fun F /\ A C_ dom F ) ->
                     ( A. x e. A ( F ` x ) = B <-> A C_ ( `' F " { B } ) ) ) $=
      ( wfun cdm wss wa cfv wceq wral cima csn ccnv wcel funimass4 elsnc ralbii
      cv fvex syl6rbb funimass3 bitrd ) DEBDFGHZASZDIZCJZABKZDBLCMZGZBDNUILGUDU
      JUFUIOZABKUHABUIDPUKUGABUFCUEDTQRUABUIDUBUC $.
      $( [8-Mar-2007] $)
  $}


  $( Another proof of ~ fvimacnv , based on ~ funimass3 .  If ~ funimass3 is
     ever proved directly, as opposed to using ~ funimacnv pointwise, then the
     proof of ~ funimacnv should be replaced with this one.  (Contributed by
     Raph Levien, 20-Nov-2006.) $)
  fvimacnvALT $p |- ( ( Fun F /\ A e. dom F ) ->
                    ( ( F ` A ) e. B <-> A e. ( `' F " B ) ) ) $=
    ( wfun cdm wcel wa csn cima wss ccnv cfv wb snssi funimass3 sylan2 fvex wfn
    snss wceq eqid df-fn biimpri mpan2 fnsnfv sylan sseq1d syl5bb snssg 3bitr4d
    adantl ) CDZACEZFZGZCAHZIZBJZUPCKBIZJZACLZBFZAUSFZUNULUPUMJURUTMAUMNUPBCOPV
    BVAHZBJUOURVABACQSUOVDUQBULCUMRZUNVDUQTULUMUMTZVEUMUAVEULVFGCUMUBUCUDUMACUE
    UFUGUHUNVCUTMULAUSUMUIUKUJ $.
    $( [20-Nov-2006] $)

  $( Membership in the preimage of a set under a function.  (Contributed by
     Jeff Madsen, 2-Sep-2009.) $)
  elpreima $p |- ( F Fn A -> ( B e. ( `' F " C )
                              <-> ( B e. A /\ ( F ` B ) e. C ) ) ) $=
    ( wfn ccnv cima wcel cfv wa cdm cnvimass sseli fndm eleq2d syl5ib fvimacnvi
    wfun fnfun sylan ex jcad wb fvimacnv funfni biimpd expimpd impbid ) DAEZBDF
    CGZHZBAHZBDICHZJUIUKULUMUKBDKZHUIULUJUNBDCLMUIUNABADNOPUIUKUMUIDRUKUMADSBCD
    QTUAUBUIULUMUKUIULJUMUKUMUKUCABDBCDUDUEUFUGUH $.
    $( [2-Sep-2009] $)

  ${
    $d x F $.  $d x A $.  $d x B $.
    $( Preimage of a union.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    unpreima $p |- ( Fun F -> ( `' F " ( A u. B ) )
                                  = ( ( `' F " A ) u. ( `' F " B ) ) ) $=
      ( vx wfun cdm wfn ccnv cun cima wceq funfn cv wcel wa wo wb elun elpreima
      cfv anbi2i andi bitri a1i orbi12d syl5bb 3bitr4d eqrdv sylbi ) CECCFZGZCH
      ZABIZJZULAJZULBJZIZKCLUKDUNUQUKDMZUJNZURCTZUMNZOZUSUTANZOZUSUTBNZOZPZURUN
      NURUQNZVBVGQUKVBUSVCVEPZOVGVAVIUSUTABRUAUSVCVEUBUCUDUJURUMCSVHURUONZURUPN
      ZPUKVGURUOUPRUKVJVDVKVFUJURACSUJURBCSUEUFUGUHUI $.
      $( [2-Sep-2009] $)

    $( Preimage of an intersection.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    inpreima $p |- ( Fun F -> ( `' F " ( A i^i B ) )
                                  = ( ( `' F " A ) i^i ( `' F " B ) ) ) $=
      ( vx wfun cdm wfn ccnv cin cima wceq funfn cv wcel wa cfv anbi2i elpreima
      wb elin a1i anandi syl6bb anbi12d 3bitr4d syl6bbr eqrdv sylbi ) CECCFZGZC
      HZABIZJZUKAJZUKBJZIZKCLUJDUMUPUJDMZUMNZUQUNNZUQUONZOZUQUPNUJUQUINZUQCPZUL
      NZOZVBVCANZOZVBVCBNZOZOZURVAUJVEVBVFVHOZOZVJVEVLSUJVDVKVBVCABTQUAVBVFVHUB
      UCUIUQULCRUJUSVGUTVIUIUQACRUIUQBCRUDUEUQUNUOTUFUGUH $.
      $( [2-Sep-2009] $)

    $( The preimage of a restricted function.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    respreima $p |- ( Fun F -> ( `' ( F |` B ) " A )
                                    = ( ( `' F " A ) i^i B ) ) $=
      ( vx wfun cres ccnv cima cin cdm wfn cv wcel wb cfv wa bitri syl elpreima
      elin funfn ancom anbi1i fvres eleq1d adantl pm5.32i a1i an32 syl6bb fnfun
      wceq funres dmres jctir df-fn sylibr anbi1d syl5bb 3bitr4d sylbi eqrdv )
      CEZDCBFZGAHZCGAHZBIZVCCCJZKZDLZVEMZVJVGMZNCUAVIVJBVHIZMZVJVDOZAMZPZVJVHMZ
      VJCOZAMZPZVJBMZPZVKVLVIVQVRWBPZVTPZWCVQWENVIVQWDVPPWEVNWDVPVNWBVRPWDVJBVH
      TWBVRUBQUCWDVPVTWBVPVTNVRWBVOVSAVJBCUDUEUFUGQUHVRWBVTUIUJVIVDVMKZVKVQNVIV
      DEZVDJVMULZPWFVIWGWHVIVCWGVHCUKBCUMRCBUNUOVDVMUPUQVMVJAVDSRVLVJVFMZWBPVIW
      CVJVFBTVIWIWAWBVHVJACSURUSUTVAVB $.
      $( [2-Sep-2009] $)
  $}

  $( The pre-image of the codomain of a mapping is the mapping's domain.
     (Contributed by FL, 25-Jan-2007.) $)
  fimacnv $p |- ( F : A --> B -> ( `' F " B ) = A ) $=
    ( ccnv cima crn imassrn cdm dfdm4 fdm wss ssid a1i eqsstrd syl5eqssr syl5ss
    wf frn wfun wb ffun syl5sseqr funimass3 syl2anc mpbid eqssd ) ABCQZCDZBEZAU
    GUIUHFZAUHBGUGUJCHZACIUGUKAAABCJZAAKUGALZMNOPUGCAEZBKZAUIKZUGUNCFBCAGABCRPU
    GCSAUKKUOUPTABCUAUGAAUKUMULUBABCUCUDUEUF $.
    $( [25-Jan-2007] $)

  $( Ordered pair with function value.  Part of Theorem 4.3(i) of [Monk1]
     p. 41. $)
  fnopfv $p |- ( ( F Fn A /\ B e. A ) -> <. B , ( F ` B ) >. e. F ) $=
    ( cfv cop wcel funfvop funfni ) BBCDECFABCBCGH $.
    $( [30-Sep-2004] $)

  ${
    $d x y F $.  $d x A $.
    $( A function's value belongs to its range. $)
    fvelrn $p |- ( ( Fun F /\ A e. dom F ) -> ( F ` A ) e. ran F ) $=
      ( vx vy wfun cdm wcel cfv cv wa wi wceq eleq1 anbi2d fveq2 eleq1d imbi12d
      crn cop wex funfvop vex opeq1 cla4ev syl fvex elrn2 sylibr vtoclg anabsi7
      ) BEZABFZGZABHZBRZGZUKCIZULGZJZUQBHZUOGZKUKUMJZUPKCAULUQALZUSVBVAUPVCURUM
      UKUQAULMNVCUTUNUOUQABOPQUSDIZUTSZBGZDTZVAUSUQUTSZBGZVGUQBUAVFVIDUQCUBVDUQ
      LVEVHBVDUQUTUCPUDUEDUTBUQBUFUGUHUIUJ $.
      $( [14-Oct-1996] $)
  $}

  $( A function's value belongs to its range. $)
  fnfvelrn $p |- ( ( F Fn A /\ B e. A ) -> ( F ` B ) e. ran F ) $=
    ( cfv crn wcel fvelrn funfni ) BCDCEFABCBCGH $.
    $( [15-Oct-1996] $)

  $( A function's value belongs to its codomain. $)
  ffvelrn $p |- ( ( F : A --> B /\ C e. A ) -> ( F ` C ) e. B ) $=
    ( wf wcel wa cfv crn wfn ffn fnfvelrn sylan wi frn sseld adantr mpd ) ABDEZ
    CAFZGCDHZDIZFZUABFZSDAJTUCABDKACDLMSUCUDNTSUBBUAABDOPQR $.
    $( [12-Aug-1999] $)

  ${
    ffvrni.1 $e |- F : A --> B $.
    $( A function's value belongs to its codomain. $)
    ffvelrni $p |- ( C e. A -> ( F ` C ) e. B ) $=
      ( wf wcel cfv ffvelrn mpan ) ABDFCAGCDHBGEABCDIJ $.
      $( [6-Apr-2005] $)
  $}

  ${
    $d x y A $.  $d x y F $.  $d x ps $.  $d y ph $.
    rexrn.1 $e |- ( x = ( F ` y ) -> ( ph <-> ps ) ) $.
    $( Restricted existential quantification over the range of a function.
       (Contributed by Mario Carneiro, 24-Dec-2013.) $)
    rexrn $p |- ( F Fn A -> ( E. x e. ran F ph <-> E. y e. A ps ) ) $=
      ( wfn crn wrex cv wcel wa cfv wceq fvelrnb biimpa wb eqcoms rexlimdva syl
      biimpcd reximdv syl5com wi fnfvelrn rcla4ev ex impbid ) FEHZACFIZJZBDEJZU
      JAUMCUKUJCKZUKLZMDKZFNZUNOZDEJZAUMUJUOUSDEUNFPQAURBDEURABABRUNUQGSUBUCUDT
      UJBULDEUJUPELMUQUKLZBULUEEUPFUFUTBULABCUQUKGUGUHUATUI $.
      $( [24-Dec-2013] $)

    $( Restricted universal quantification over the range of a function.
       (Contributed by Mario Carneiro, 24-Dec-2013.) $)
    ralrn $p |- ( F Fn A -> ( A. x e. ran F ph <-> A. y e. A ps ) ) $=
      ( wfn crn wral cv wcel wa cfv wi fnfvelrn rcla4v syl ralrimdva wrex wb ex
      wceq fvelrnb biimpa r19.29 eqcoms biimparc rexlimivw syl5com impbid ) FEH
      ZACFIZJZBDEJZULUNBDEULDKZELMUPFNZUMLUNBOEUPFPABCUQUMGQRSULUOACUMULCKZUMLZ
      MUQURUCZDETZUOAULUSVADEURFUDUEUOVAAUOVAMBUTMZDETABUTDEUFVBADEUTABABUAURUQ
      GUGUHUIRUBUJSUK $.
      $( [24-Dec-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.
    $( A function expressed as the range of another function.  (Contributed by
       Mario Carneiro, 22-Jun-2013.) $)
    fnasrn $p |- ( F Fn A -> F = ran { <. x , y >. |
        ( x e. A /\ y = <. x , ( F ` x ) >. ) } ) $=
      ( vz vw cv cfv cop wceq wrex wcel wa wb wal vex imp wi syl cvv wfn opeldm
      cab copab crn fndm eleq2d syl5ib fnfun funopfv opeq2d eqcomd fveq2 opeq12
      cdm wfun mpdan eqeq2d rcla4ev syl2anc fnopfv eleq1a rexlimdva impbid opex
      ex eqeq1 rexbidv elab syl6bbr alrimivv wrel fnrel cxp wss opelxp mpbir2an
      fvex eleq1 mpbiri abssi df-rel mpbir eqrel sylancl mpbird rnopab2 syl6eqr
      rexlimivw ) DCUAZDBGZAGZWLDHZIZJZACKZBUCZWLCLZWOMABUDUEWJDWQJZEGZFGZIZDLZ
      XBWQLZNZFOEOZWJXEEFWJXCXBWNJZACKZXDWJXCXHWJXCXHWJXCMZWTCLZXBWTWTDHZIZJZXH
      WJXCXJXCWTDUOZLWJXJWTXADEPUBWJXNCWTCDUFUGUHQXIXLXBXIXKXAWTWJXCXKXAJZWJDUP
      XCXORCDUIWTXADFPUJSQUKULXGXMAWTCWLWTJZWNXLXBXPWMXKJWNXLJWLWTDUMWLWMWTXKUN
      UQURUSUTVFWJXGXCACWJWRMWNDLXGXCRCWLDVAWNDXBVBSVCVDWPXHBXBWTXAVEWKXBJWOXGA
      CWKXBWNVGVHVIVJVKWJDVLWQVLZWSXFNCDVMXQWQTTVNZVOWPBXRWOWKXRLZACWOXSWNXRLZX
      TWLTLWMTLWLWMTTWLDVRZVPAPYAVQWKWNXRVSVTWIWAWQWBWCEFDWQWDWEWFABCWNWGWH $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y D $.  $d y B $.
    f0cl.1 $e |- F : A --> B $.
    f0cl.2 $e |- (/) e. B $.
    $( Unconditional closure of a function when the range includes the empty
       set.  (Contributed by Mario Carneiro, 12-Sep-2013.) $)
    f0cli $p |- ( F ` C ) e. B $=
      ( wcel cfv ffvelrni wn cdm eleq2i notbii c0 ndmfv syl6eqel sylbir pm2.61i
      fdmi ) CAGZCDHZBGZABCDEITJCDKZGZJZUBUDTUCACABDESLMUEUANBCDOFPQR $.
      $( [12-Sep-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The restriction of a function to a set exists.  Compare Proposition 6.17
       of [TakeutiZaring] p. 28.  (Revised by Mario Carneiro, 22-Jun-2013.) $)
    resfunexg $p |- ( ( Fun A /\ B e. C ) -> ( A |` B ) e. _V ) $=
      ( vx vy wfun wcel cres cvv cv cdm cfv cop wceq wa copab funopab4 dmresexg
      cima wfn funimaexg sylancr crn funres funfn fnasrn sylbi syl imadmrn wral
      opex a1i rgen eqid fnopab2g mpbi fndm ax-mp imaeq2i eqtr3i syl6eq syl5ibr
      eleq1d imp ) AFZBCGZABHZIGZVFVHVEDJZVGKZGZEJVIVIVGLZMZNODEPZVJSZIGZVFVNFV
      JIGVPVKDEVMQABCRVNVJIUAUBVEVGVOIVEVGVNUCZVOVEVGFZVGVQNZBAUDVRVGVJTVSVGUED
      EVJVGUFUGUHVNVNKZSVQVOVNUIVTVJVNVNVJTZVTVJNVMIGZDVJUJWAWBDVJWBVKVIVLUKULU
      MDEVJVMVNVNUNUOUPVJVNUQURUSUTVAVCVBVD $.
      $( [22-Jun-2013] $) $( [7-Apr-1995] $)
  $}

  $( The restriction of a function to a set exists.  Compare Proposition 6.17
     of [TakeutiZaring] p. 28.  This version has a shorter proof than
     ~ resfunexg but requires ~ ax-pow . $)
  resfunexgALT $p |- ( ( Fun A /\ B e. C ) -> ( A |` B ) e. _V ) $=
    ( wfun wcel cres cdm cvv crn cxp dmresexg adantl df-ima funimaexg syl5eqelr
    wa cima jca xpexg wss wrel relres relssdmrn ax-mp ssexg mpan 3syl ) ADZBCEZ
    PZABFZGZHEZUKIZHEZPULUNJZHEZUKHEZUJUMUOUIUMUHABCKLUJUNABQHABMABCNORULUNHHSU
    KUPTZUQURUKUAUSABUBUKUCUDUKUPHUEUFUG $.
    $( [12-Sep-2006] $) $( [7-Apr-1995] $)

  $( Existence of a composition when the first member is a function. $)
  cofunexg $p |- ( ( Fun A /\ B e. C ) -> ( A o. B ) e. _V ) $=
    ( wfun wcel wa ccom cdm crn cxp wss wrel relco relssdmrn ax-mp dmcoss ssexg
    cvv sylancr rnexg dmexg adantl cres rnco resfunexg sylan2 syl xpexg syl2anc
    syl5eqel ) ADZBCEZFZABGZUNHZUNIZJZKZUQREZUNREUNLURABMUNNOUMUOREZUPREUSULUTU
    KULUOBHZKVAREUTABPBCUAUOVARQSUBUMUPABIZUCZIZRABUDUMVCREZVDREULUKVBREVEBCTAV
    BRUEUFVCRTUGUJUOUPRRUHUIUNUQRQS $.
    $( [8-Oct-2007] $)

  $( Existence of a composition when the second member is one-to-one. $)
  cofunex2g $p |- ( ( A e. V /\ Fun `' B ) -> ( A o. B ) e. _V ) $=
    ( ccnv wfun wcel ccom cvv wa cnvexg cofunexg sylan2 cnvco cocnvcnv2 3eqtrri
    cocnvcnv1 syl5eqel syl ancoms ) BDZEZACFZABGZHFZUAUBITADZGZHFZUDUBUAUEHFUGA
    CJTUEHKLUGUCUFDZHUHUEDZTDGUIBGUCTUEMUIBNABPOUFHJQRS $.
    $( [8-Oct-2007] $)

  $( If the domain of a function is a set, the function is a set.  Theorem
     6.16(1) of [TakeutiZaring] p. 28.  This theorem is derived using the Axiom
     of Replacement in the form of ~ resfunexg .  (The proof was shortened by
     Andrew Salmon, 17-Sep-2011.) $)
  fnex $p |- ( ( F Fn A /\ A e. B ) -> F e. _V ) $=
    ( wfn wcel wrel cdm cres cvv fnrel adantr wfun wceq eleq1a impcom resfunexg
    wa df-fn sylan2 anassrs sylanb resdm eleq1d biimpa syl2anc ) CADZABEZQCFZCC
    GZHZIEZCIEZUFUHUGACJKUFCLZUIAMZQUGUKCARUMUNUGUKUNUGQUMUIBEZUKUGUNUOABUINOCU
    IBPSTUAUHUKULUHUJCICUBUCUDUE $.
    $( [18-Sep-2011] $) $( [14-Aug-1994] $)

  $( If the domain of a function is a set, the function is a set.  Theorem
     6.16(1) of [TakeutiZaring] p. 28.  This theorem is derived using the Axiom
     of Replacement in the form of ~ funimaexg . $)
  fnexALT $p |- ( ( F Fn A /\ A e. B ) -> F e. _V ) $=
    ( wfn wcel wa cdm crn cxp wss cvv fnrel relssdmrn syl adantr eleq1d biimpar
    wrel cima syl2anc fndm fnfun funimaexg sylan imadmrn imaeq2d syl5eqr syldan
    wfun xpexg ssexg ) CADZABEZFZCCGZCHZIZJZUQKEZCKEULURUMULCRURACLCMNOUNUOBEZU
    PKEZUSULUTUMULUOABACUAZPQULUMCASZKEZVAULCUIUMVDACUBCABUCUDULVAVDULUPVCKULUP
    CUOSVCCUEULUOACVBUFUGPQUHUOUPBKUJTCUQKUKT $.
    $( [11-Sep-2006] $) $( [14-Aug-1994] $)

  $( If the domain of a function exists, so the function.  Part of Theorem
     4.15(v) of [Monk1] p. 46.  This theorem is derived using the Axiom of
     Replacement in the form of ~ fnex .  (Note:  Any resemblance between
     F.U.N.E.X. and "Have You Any Eggs" is purely a coincidence originated by
     Swedish chefs.) $)
  funex $p |- ( ( Fun F /\ dom F e. B ) -> F e. _V ) $=
    ( wfun cdm wfn wcel cvv funfn fnex sylanb ) BCBBDZEKAFBGFBHKABIJ $.
    $( [9-Sep-2006] $) $( [11-Nov-1995] $)

  ${
    $d x y A $.
    opabex.1 $e |- A e. _V $.
    opabex.2 $e |- ( x e. A -> E* y ph ) $.
    $( Existence of a function expressed as class of ordered pairs. $)
    opabex $p |- { <. x , y >. | ( x e. A /\ ph ) } e. _V $=
      ( cv wcel wa copab wfun cdm cvv wmo funopab moanimv mpbir mpgbir dmopabss
      wi ssexi funex mp2an ) BGDHZAIZBCJZKZUFLZMHUFMHUGUECNZBUEBCOUIUDACNTFUDAC
      PQRUHDEABCDSUAMUFUBUC $.
      $( [21-Jul-1996] $)
  $}

  ${
    $d x y A $.  $d y B $.
    opabex2.1 $e |- A e. _V $.
    $( Existence of a function expressed as class of ordered pairs. $)
    opabex2 $p |- { <. x , y >. | ( x e. A /\ y = B ) } e. _V $=
      ( cv wceq wmo wcel moeq a1i opabex ) BFDGZABCEMBHAFCIBDJKL $.
      $( [22-Apr-2005] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Existence of a function expressed as class of ordered pairs. $)
    opabex2g $p |- ( A e. V ->
                      { <. x , y >. | ( x e. A /\ y = B ) } e. _V ) $=
      ( wcel cv wceq wa copab wfun cdm cvv funopab4 dmopabss ssexg mpan sylancr
      wss funex ) CEFZAGCFZBGDHZIABJZKUDLZMFZUDMFUBABDNUECSUAUFUCABCOUECEPQMUDT
      R $.
      $( [9-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.
    fopabex2.1 $e |- A e. _V $.
    fopabex2.3 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    $( Existence of a function expressed as class of ordered pairs. $)
    fopabex2 $p |- F e. _V $=
      ( cv wcel wceq wa copab cvv opabex2 eqeltri ) EAHCIBHDJKABLMGABCDFNO $.
      $( [30-Jul-2007] $)
  $}

  $( If the domain of a function exists, so does its range.  Part of Theorem
     4.15(v) of [Monk1] p. 46.  This theorem is derived using the Axiom of
     Replacement in the form of ~ funex . $)
  funrnex $p |- ( dom F e. B -> ( Fun F -> ran F e. _V ) ) $=
    ( wfun cdm wcel cvv crn funex ex rnexg syl6com ) BCZBDAEZBFEZBGFELMNABHIBFJ
    K $.
    $( [11-Nov-1995] $)

  ${
    $d ph w v $.  $d x y z w v $.
    $( A version of the Axiom of Replacement.  Normally ` ph ` would have free
       variables ` x ` and ` y ` .  Axiom 6 of [Kunen] p. 12.  The Separation
       Scheme ~ ax-sep cannot be derived from this version and must be stated
       as a separate axiom in an axiom system (such as Kunen's) that uses this
       version in place of our ~ ax-rep . $)
    zfrep6 $p |- ( A. x e. z E! y ph -> E. w A. x e. z E. y e. w ph ) $=
      ( vv cv wral wcel wa cvv wrex wex wceq sylibr cab vex wi wal ax-17 ralimi
      weu copab crn wfun crab euex rabid2 19.42v dmopab df-rab 3eqtr4i syl6reqr
      cdm abbii syl6eqel wmo eumo imim2i moanimv df-ral funopab 3imtr4i funrnex
      alimi sylc hbra1 eleq2d opabid opelrn sylbir ex impac eximi abeq2i df-rex
      cop syl6bir ralrimi hbopab1 hbrn hbeleq hbopab2 rexeqf ralbid cla4egf ) A
      CUBZBDGZHZBGZWHIZAJZBCUCZUDZKIZACWNLZBWHHZACEGZLZBWHHZEMWIWMUNZKIWMUEZWOW
      IXAWHKWIWHACMZBWHUFZXAWIXCBWHHWHXDNWGXCBWHACUGUAXCBWHUHOWLCMZBPWKXCJZBPXA
      XDXEXFBWKACUIUOWLBCUJZXCBWHUKULUMZDQUPWKWGRZBSWLCUQZBSWIXBXIXJBXIWKACUQZR
      XJWGXKWKACURUSWKACUTOVEWGBWHVAWLBCVBVCKWMVDVFWIWPBWHWGBWHVGWIWKWJXAIZWPWI
      XAWHWJXHVHXECGZWNIZAJZCMXLWPWLXOCWKAXNWKAXNWLWJXMVQWMIXNWLBCVIWJXMWMBQCQV
      JVKVLVMVNXEBXAXGVOACWNVPVCVRVSWTWQEFWNKFGZWNIETWQETWRWNNWSWPBWHBEWNBEWMWL
      BCEVTWAWBACFWRWNXPWRICTCFWMWLBCFWCWAWDWEWFVF $.
      $( [10-Oct-2003] $)
  $}

  $( If the domain of a mapping is a set, the function is a set. $)
  fex $p |- ( ( F : A --> B /\ A e. C ) -> F e. _V ) $=
    ( wf wfn wcel cvv ffn fnex sylan ) ABDEDAFACGDHGABDIACDJK $.
    $( [11-Sep-2006] $) $( [3-Oct-1999] $)

  $( If the domain of an onto function exists, so does its codomain. $)
  fornex $p |- ( A e. C -> ( F : A -onto-> B -> B e. _V ) ) $=
    ( wfo wcel cvv cdm crn wfun wi fofun funrnex com12 syl wceq fof fdm eleq1d
    wf forn 3imtr3d ) ABDEZACFZBGFZUCDHZCFZDIZGFZUDUEUCDJZUGUIKABDLUGUJUICDMNOU
    CUFACUCABDTUFAPABDQABDROSUCUHBGABDUASUBN $.
    $( [23-Jul-2004] $)

  $( If the codomain of a one-to-one function exists, so does its domain.  This
     theorem is equivalent to the Axiom of Replacement ~ ax-rep . $)
  f1dmex $p |- ( ( F : A -1-1-> B /\ B e. C ) -> A e. _V ) $=
    ( wf1 wcel cvv crn wss wf f1f frn syl ssexg sylan ex ccnv wfo wf1o f1f1orn
    f1ocnv f1ofo 3syl fornex syl5com syld imp ) ABDEZBCFZAGFZUHUIDHZGFZUJUHUIUL
    UHUKBIZUIULUHABDJUMABDKABDLMUKBCNOPUHUKADQZRZULUJUHAUKDSUKAUNSUOABDTAUKDUAU
    KAUNUBUCUKAGUNUDUEUFUG $.
    $( [12-Sep-2006] $) $( [4-Sep-2004] $)

  ${
    $d f x y z A $.  $d f y z B $.
    eufnfv.1 $e |- A e. _V $.
    eufnfv.2 $e |- B e. _V $.
    $( A function is uniquely determined by its values. $)
    eufnfv $p |- E! f ( f Fn A /\ A. x e. A ( f ` x ) = B ) $=
      ( vz vy cv wfn cfv wceq wral wa weu wb wal wcel mpan2 cvv weq wex opabex2
      copab eqeq2 bibi2d albidv cla4ev eqid fnopab2 fneq1 mpbiri pm4.71ri ax-17
      hbopab1 eqfnfv2f fvopab2 eqeq2d ralbiia syl6bb pm5.32i bitr2i df-eu mpbir
      wel mpg ) DIZBJZAIZVGKZCLZABMZNZDOVMDGUAZPZDQZGUBZVMVGVIBRZHICLNZAHUDZLZP
      ZVQDVPWBDQGVTAHBCEUCGIZVTLZVOWBDWDVNWAVMWCVTVGUEUFUGUHWAVHWANVMWAVHWAVHVT
      BJZAHBCVTFVTUIUJZBVGVTUKULUMVHWAVLVHWAVJVIVTKZLZABMZVLVHWEWAWIPWFAGBVGVTG
      DVEAUNVSAHGUOUPSWHVKABVRWGCVJVRCTRWGCLFAHBCTUQSURUSUTVAVBVFVMDGVCVD $.
      $( [31-Aug-2011] $)
  $}

  $( Alternate definition of a mapping. $)
  dff2 $p |- ( F : A --> B <-> ( F Fn A /\ F C_ ( A X. B ) ) ) $=
    ( wf wfn cxp wss wa ffn jca crn rnss rnxpss sstr sylancl anim2i df-f sylibr
    fssxp impbii ) ABCDZCAEZCABFZGZHZUAUBUDABCIABCSJUEUBCKZBGZHUAUDUGUBUDUFUCKZ
    GUHBGUGCUCLABMUFUHBNOPABCQRT $.
    $( [14-Nov-2007] $)

  ${
    $d f g x y z A $.  $d f g x y z B $.  $d x y z F $.
    $( Alternate definition of a mapping. $)
    dff3 $p |- ( F : A --> B <->
              ( F C_ ( A X. B ) /\ A. x e. A E! y x F y ) ) $=
      ( vz cxp wss cv wbr weu wa wcel wex adantr sylibr sylanbrc sstr wi syl6
      wf wral fssxp wmo cfv cop wfun cdm ffun fdm biimpar funfvop syl2anc df-br
      eleq2d fvex breq2 cla4ev syl funmo eu5 ralrimiva jca wfn crn wceq wal cvv
      wrel xpss mpan2 df-rel df-ral eumo imim2i adantl wn syl5bi opelxp simplbi
      ssel vex exlimdv con3d exmo ori pm2.61d ex alimdv imp dffun6 dmss sylancl
      dmxpss breq1 eubidv rcla4cv euex eldm ssrdv anim12i eqss rnss rnxpss df-f
      df-fn impbii ) CDEUAZECDGZHZAIZBIZEJZBKZACUBZLZXHXJXOCDEUCXHXNACXHXKCMZLZ
      XMBNZXMBUDZXNXRXKXKEUEZEJZXSXRXKYAUFEMZYBXREUGZXKEUHZMZYCXHYDXQCDEUIZOXHY
      FXQXHYECXKCDEUJUOUKXKEULUMXKYAEUNPXMYBBYAXKEUPXLYAXKEUQURUSXHXTXQXHYDXTYG
      ABEUTUSOXMBVAQVBVCXPECVDZEVEZDHZXHXPYDYECVFZYHXPEVIZXTAVGZYDXJYLXOXJEVHVH
      GZHZYLXJXIYNHYOCDVJEXIYNRVKEVLPOXJXOYMXOXQXNSZAVGXJYMXNACVMXJYPXTAXJYPXTX
      JYPLXQXTYPXQXTSXJXNXTXQXMBVNVOVPXJXQVQZXTSYPXJYQXSVQXTXJXSXQXJXMXQBXJXMXK
      XLUFZXIMZXQXMYREMXJYSXKXLEUNEXIYRWAVRYSXQXLDMXKXLCDBWBVSVTTWCWDXSXTXMBWEW
      FTOWGWHWIVRWJABEWKQXPYECHZCYEHZLYKXJYTXOUUAXJYEXIUHZHUUBCHYTEXIWLCDWNYEUU
      BCRWMXOFCYEXOFIZCMUUCXLEJZBKZUUCYEMZXNUUEAUUCCXKUUCVFXMUUDBXKUUCXLEWOWPWQ
      UUEUUDBNUUFUUDBWRBUUCEFWBWSPTWTXAYECXBPECXFQXJYJXOXJYIXIVEZHUUGDHYJEXIXCC
      DXDYIUUGDRWMOCDEXEQXG $.
      $( [20-Mar-2007] $)

    $( Alternate definition of a mapping. $)
    dff4 $p |- ( F : A --> B <->
              ( F C_ ( A X. B ) /\ A. x e. A E! y e. B x F y ) ) $=
      ( wf cxp wss cv wbr weu wral wa wreu dff3 wcel cop df-br ssel vex simprbi
      opelxp syl6 syl5bi pm4.71rd eubidv df-reu syl6bbr ralbidv pm5.32i bitri )
      CDEFECDGZHZAIZBIZEJZBKZACLZMUMUPBDNZACLZMABCDEOUMURUTUMUQUSACUMUQUODPZUPM
      ZBKUSUMUPVBBUMUPVAUPUNUOQZEPZUMVAUNUOERUMVDVCULPZVAEULVCSVEUNCPVAUNUOCDBT
      UBUAUCUDUEUFUPBDUGUHUIUJUK $.
      $( [20-Mar-2007] $)

    $( An onto mapping expressed in terms of function values. $)
    dffo3 $p |- ( F : A -onto-> B <-> ( F : A --> B /\
                  A. y e. B E. x e. A y = ( F ` x ) ) ) $=
      ( wfo wf crn wceq wa cv cfv wrex wral dffo2 cab wb wcel wal wi wfn fnrnfv
      ffn eqeq1d simpr ffvelrn adantr eqeltrd exp31 rexlimdv biantrurd syl6rbbr
      syl dfbi2 albidv abeq1 df-ral 3bitr4g bitrd pm5.32i bitri ) CDEFCDEGZEHZD
      IZJVBBKZAKZELZIZACMZBDNZJCDEOVBVDVJVBVDVIBPZDIZVJVBECUAZVDVLQCDEUCVMVCVKD
      ABCEUBUDUMVBVIVEDRZQZBSVNVITZBSVLVJVBVOVPBVBVPVIVNTZVPJVOVBVQVPVBVHVNACVB
      VFCRZVHVNVBVRJZVHJVEVGDVSVHUEVSVGDRVHCDVFEUFUGUHUIUJUKVIVNUNULUOVIBDUPVIB
      DUQURUSUTVA $.
      $( [29-Oct-2006] $)

    $( Alternate definition of an onto mapping. $)
    dffo4 $p |- ( F : A -onto-> B <->
                ( F : A --> B /\ A. y e. B E. x e. A x F y ) ) $=
      ( wfo wf cv wbr wrex wral wa crn wceq dffo2 simpl wcel wex vex wi syl5bbr
      elrn eleq2 biimpar adantll wfn ffn fnbr syl ancrd eximdv syl6ibr ad2antrr
      ex df-rex mpd ralrimiva jca sylbi cfv fnbrfvb eqcom syl6ib sylan reximdva
      biimprd ralimdv imdistani dffo3 sylibr impbii ) CDEFZCDEGZAHZBHZEIZACJZBD
      KZLZVLVMEMZDNZLZVSCDEOWBVMVRVMWAPWBVQBDWBVODQZLVPARZVQWAWCWDVMWAWDWCWDVOV
      TQWAWCAVOEBSZUBVTDVOUCUAUDUEVMWDVQTWAWCVMWDVNCQZVPLZARVQVMVPWGAVMVPWFVMEC
      UFZVPWFTCDEUGZWHVPWFCVNVOEUHUNUIUJUKVPACUOULUMUPUQURUSVSVMVOVNEUTZNZACJZB
      DKZLVLVMVRWMVMVQWLBDVMVPWKACVMWHWFVPWKTWIWHWFLZVPWJVONZWKWNWOVPCVNVOEWEVA
      VFWJVOVBVCVDVEVGVHABCDEVIVJVK $.
      $( [20-Mar-2007] $)

    $( Alternate definition of an onto mapping. $)
    dffo5 $p |- ( F : A -onto-> B <->
                ( F : A --> B /\ A. y e. B E. x x F y ) ) $=
      ( wfo wf cv wbr wrex wral wa wex dffo4 rexex ralimi anim2i wcel wfn wi ex
      ffn fnbr syl ancrd eximdv df-rex syl6ibr ralimdv imdistani impbii bitri )
      CDEFCDEGZAHZBHZEIZACJZBDKZLZUMUPAMZBDKZLZABCDENUSVBURVAUMUQUTBDUPACOPQUMV
      AURUMUTUQBDUMUTUNCRZUPLZAMUQUMUPVDAUMUPVCUMECSZUPVCTCDEUBVEUPVCCUNUOEUCUA
      UDUEUFUPACUGUHUIUJUKUL $.
      $( [20-Mar-2007] $)

    $( A relation equivalent to the existence of an onto mapping.  The
       right-hand ` f ` is not necessarily a function. $)
    exfo $p |- ( E. f f : A -onto-> B <->
           E. f ( A. x e. A E! y e. B x f y /\ A. x e. B E. y e. A y f x ) ) $=
      ( vg cv wfo wex wbr wreu wral wrex wa wf dffo4 wss dff4 sylibr foeq1 wceq
      cxp simprbi anim1i sylbi eximi cin crn wcel brinxp reubidva ralimia inss2
      biimpd jctil rninxp biimpri anim12i dffo2 vex inex1 cla4ev exlimiv cbvexv
      syl sylib impbii ) CDEGZHZEIZAGZBGZVHJZBDKZACLZVLVKVHJBCMADLZNZEIZVIVQEVI
      CDVHOZVPNVQBACDVHPVSVOVPVSVHCDUBZQVOABCDVHRUCUDUEUFVRCDFGZHZFIZVJVQWCEVQC
      DVHVTUGZHZWCVQCDWDOZWDUHDUAZNWEVOWFVPWGVOWDVTQZVKVLWDJZBDKZACLZNWFVOWKWHV
      NWJACVKCUIZVNWJWLVMWIBDVKVLCDVHUJUKUNULVHVTUMUOABCDWDRSWGVPBACDVHUPUQURCD
      WDUSSWBWEFWDVHVTEUTVACDWAWDTVBVEVCWBVIFECDWAVHTVDVFVG $.
      $( [20-Mar-2007] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d B x y $.  $d C x y $.
    $( Property of a surjective function.  (Contributed by Jeff Madsen,
       4-Jan-2011.) $)
    foelrn $p |- ( ( F : A -onto-> B /\ C e. B )
                                  -> E. x e. A C = ( F ` x ) ) $=
      ( vy wfo cv cfv wceq wrex wral wcel wf dffo3 simprbi eqeq1 rcla4cva sylan
      rexbidv ) BCEGZFHZAHEIZJZABKZFCLZDCMDUCJZABKZUABCENUFAFBCEOPUEUHFDCUBDJUD
      UGABUBDUCQTRS $.
      $( [4-Jan-2011] $)
  $}

  ${
    $d F x y z $.  $d G x y z $.  $d A y z $.  $d B x y z $.  $d C x y z $.
    $( If a composition of two functions is surjective, then the function on
       the left is surjective.  (Contributed by Jeff Madsen, 16-Jun-2011.) $)
    foco2 $p |- ( ( F : B --> C /\ G : A --> B /\
                        ( F o. G ) : A -onto-> C ) -> F : B -onto-> C ) $=
      ( vy vx vz wf ccom wfo w3a cv cfv wceq wrex wral simp1 wa wcel wfun fvco3
      foelrn ffvelrn adantll 3expa sylanl1 fveq2 eqeq2d rcla4ev syl2anc rexbidv
      ffun eqeq1 syl5ibrcom rexlimdva syl5 imp anassrs ralrimiva 3impa sylanbrc
      dffo3 ) BCDIZABEIZACDEJZKZLVDFMZGMZDNZOZGBPZFCQZBCDKVDVEVGRVDVEVGVMVDVESZ
      VGSVLFCVNVGVHCTZVLVNVGVOSZVLVPVHHMZVFNZOZHAPVNVLHACVHVFUCVNVSVLHAVNVQATZS
      ZVLVSVRVJOZGBPZWAVQENZBTZVRWDDNZOZWCVEVTWEVDABVQEUDUEVDDUAZVEVTWGBCDUMWHV
      EVTWGABVQDEUBUFUGWBWGGWDBVIWDOVJWFVRVIWDDUHUIUJUKVSVKWBGBVHVRVJUNULUOUPUQ
      URUSUTVAGFBCDVCVB $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y z C $.  $d z F $.
    fopab2.1 $e |- F = { <. x , y >. | ( x e. A /\ y = C ) } $.
    $( Functionality of an ordered-pair class abstraction. $)
    fopab2 $p |- ( A. x e. A C e. B <-> F : A --> B ) $=
      ( vz wcel wral wf crn wss cv wceq cvv sylib wa wi ax-17 wfn weu elex eueq
      ralimi fnopabg copab rneqi wal wex rnopab abeq2i ra4 eleq1a imim2i exlimd
      hbra1 imp3a syl syl5bi alrimiv hbopab2 hbrn dfss2f syl5eqss df-f sylanbrc
      sylibr cdm fdm dmopab3 isset ralbii dmeqi eqeq1i 3bitr4ri hbopab1 hbxfrbi
      feq1i hbf ffvelrn adantr wb fveq1i fvopab2 syl5eq eleq1d adantll mpbid ex
      cfv ralimdaa mpd impbii ) EDIZACJZCDFKZWPFCUAZFLZDMWQWPBNZEOZBUBZACJWRWOX
      BACWOEPIZXBEDUCBEUDQUEXAABCFGUFQWPWSANZCIZXARZABUGZLZDFXGGUHWPWTXHIZWTDIZ
      SZBUIXHDMWPXKBXIXFAUJZWPXJXLBXHXFABUKULWPXFXJAWOACUQXJATWPXEWOSZXFXJSWOAC
      UMXMXEXAXJWOXAXJSXEEDWTUNUOURUSUPUTVABHXHDBHXGXFABHVBVCHNZDIZBTVDVHVECDFV
      FVGWQXCACJZWPWQFVIZCOZXPCDFVJXABUJZACJXGVIZCOXPXRXAABCVKXCXSACBEVLVMXQXTC
      FXGGVNVOVPQWQXCWOACWQCDXGKACDFXGGVSAHCDXGXFABHVQXNCIATXOATVTVRWQXERZXCWOY
      AXCRXDFWKZDIZWOYAYCXCCDXDFWAWBXEXCYCWOWCWQXEXCRZYBEDYDYBXDXGWKEXDFXGGWDAB
      CEPWEWFWGWHWIWJWLWMWN $.
      $( [29-Jan-2004] $)

    $( Inclusion of a function in a cross product. $)
    fopabssxp $p |- ( A. x e. A C e. B -> F C_ ( A X. B ) ) $=
      ( wcel wral wf cxp wss fopab2 fssxp sylbi ) EDHACICDFJFCDKLABCDEFGMCDFNO
      $.
      $( [5-Jun-2007] $)

    ${
      rnssopab.2 $e |- C e. _V $.
      $( Range of a function that is expressed as an ordered-pair class
         abstraction. $)
      rnssopab $p |- ( A. x e. A C e. B <-> ran F C_ B ) $=
        ( vz wcel wral crn wss wf fopab2 frn cv wceq cfv cvv wa hbopab1 hbxfreq
        sylbi copab hbrn ax-17 fveq1i fvopab2 mpan2 syl5eq wfn fnopab2 fnfvelrn
        hbss mpan eqeltrrd ssel syl5 ralrimi impbii ) EDJZACKZFLZDMZVCCDFNVEABC
        DEFGOCDFPUDVEVBACAIVDDAIFAIFAQZCJZBQERUAZABUEZGVHABIUBUCUFIQDJAUGUOVGEV
        DJVEVBVGVFFSZEVDVGVJVFVISZEVFFVIGUHVGETJVKERHABCETUIUJUKFCULVGVJVDJABCE
        FHGUMCVFFUNUPUQVDDEURUSUTVA $.
        $( [23-May-2006] $)

      $( Functionality of an ordered-pair class abstraction. $)
      fopab3 $p |- ( ran F C_ B <-> F : A --> B ) $=
        ( crn wss wcel wral wf rnssopab fopab2 bitr3i ) FIDJEDKACLCDFMABCDEFGHN
        ABCDEFGOP $.
        $( [23-May-2006] $)
    $}

    ${
      fopab.2 $e |- ( x e. A -> C e. B ) $.
      $( Functionality of an ordered-pair class abstraction. $)
      fopab $p |- F : A --> B $=
        ( wcel wral wf rgen fopab2 mpbi ) EDIZACJCDFKOACHLABCDEFGMN $.
        $( [19-Mar-2005] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    $( A function maps to a class to which all values belong. $)
    ffnfv $p |- ( F : A --> B <-> ( F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( vy wf wfn cv cfv wcel wral ffn ffvelrn ralrimiva jca crn wss simpl wceq
      wa wrex fvelrnb biimpd hbra1 ax-17 wi eleq1 biimpcd syl6 rexlimd ralrimiv
      ra4 sylan9 dfss3 sylibr df-f sylanbrc impbii ) BCDFZDBGZAHZDIZCJZABKZTZUS
      UTVDBCDLUSVCABBCVADMNOVEUTDPZCQZUSUTVDRVEEHZCJZEVFKVGVEVIEVFUTVHVFJZVBVHS
      ZABUAZVDVIUTVJVLABVHDUBUCVDVKVIABVCABUDVIAUEVDVABJVCVKVIUFVCABULVKVCVIVBV
      HCUGUHUIUJUMUKEVFCUNUOBCDUPUQUR $.
      $( [3-Dec-2003] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z F $.  $d x y z $.
    ffnfvf.1 $e |- ( y e. A -> A. x y e. A ) $.
    ffnfvf.2 $e |- ( y e. B -> A. x y e. B ) $.
    ffnfvf.3 $e |- ( y e. F -> A. x y e. F ) $.
    $( A function maps to a class to which all values belong.  This version of
       ~ ffnfv uses bound-variable hypotheses instead of distinct variable
       conditions. $)
    ffnfvf $p |- ( F : A --> B <-> ( F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( vz wf wfn cv cfv wcel wral wa ffnfv ax-17 hbfv hbel wceq eleq1d cbvralf
      fveq2 anbi2i bitri ) CDEJECKZILZEMZDNZICOZPUGALZEMZDNZACOZPICDEQUKUOUGUJU
      NIABCBLZCNIRFABBUIDABUHEHUPUHNARSGTUNIRUHULUAUIUMDUHULEUDUBUCUEUF $.
      $( [28-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d x B $.  $d x y F $.
    $( An upper bound for range determined by function values. $)
    fnfvrnss $p |- ( ( F Fn A /\ A. x e. A ( F ` x ) e. B ) -> ran F C_ B ) $=
      ( wfn cv cfv wcel wral wa wf crn wss ffnfv frn sylbir ) DBEAFDGCHABIJBCDK
      DLCMABCDNBCDOP $.
      $( [8-Oct-2004] $)

    $( Representation of a mapping in terms of its values. $)
    fopabfv $p |- ( F : A --> B <->
         ( F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) }
         /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( wf wfn cv cfv wcel wral wa wceq copab ffnfv dffn5 anbi1i bitri ) CDEFEC
      GZAHZEIZDJACKZLETCJBHUAMLABNMZUBLACDEOSUCUBABCEPQR $.
      $( [21-Feb-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( A necessary and sufficient condition for a restricted function.
       (Contributed by Mario Carneiro, 14-Nov-2013.) $)
    ffvresb $p |- ( Fun F -> ( ( F |` A ) : A --> B <->
        A. x e. A ( x e. dom F /\ ( F ` x ) e. B ) ) ) $=
      ( wfun cres wf cv cdm wcel cfv wa wral wss fdm cin dmres inss2 adantl wfn
      eqsstri a1i eqsstr3d ssel2 sylan fvres ffvelrn eqeltrrd jca ralrimiva crn
      wceq simpl ralimi dfss3 sylibr funfn fnssres sylanb sylan2 eleq1d syl5ibr
      simpr ralimia fnfvrnss syl2anc df-f sylanbrc ex impbid2 ) DEZBCDBFZGZAHZD
      IZJZVNDKZCJZLZABMZVMVSABVMVNBJZLZVPVRVMBVONZWAVPVMBVLIZVOBCVLOWDVONVMWDBV
      OPVODBQBVORUAUBUCBVOVNUDUEWBVNVLKZVQCWAWEVQULVMVNBDUFZSBCVNVLUGUHUIUJVKVT
      VMVKVTLZVLBTZVLUKCNZVMVTVKWCWHVTVPABMWCVSVPABVPVRUMUNABVOUOUPVKDVOTWCWHDU
      QVOBDURUSUTZWGWHWECJZABMZWIWJVTWLVKVSWKABVSWKWAVRVPVRVCWAWEVQCWFVAVBVDSAB
      CVLVEVFBCVLVGVHVIVJ $.
      $( [14-Nov-2013] $)
  $}

  ${
    $d x y A $.  $d u x B $.  $d w z B $.  $d u F $.  $d u x G $.  $d u H $.
    $d w S $.  $d y R $.  $d w R z $.  $d w T z $.  $d x v A $.  $d v T $.
    fopabco.1 $e |- R e. _V $.
    fopabco.2 $e |- S e. _V $.
    fopabco.3 $e |- T e. _V $.
    fopabco.4 $e |- ( z = R -> S = T ) $.
    fopabco.5 $e |- F = { <. x , y >. | ( x e. A /\ y = R ) } $.
    fopabco.6 $e |- G = { <. z , w >. | ( z e. B /\ w = S ) } $.
    fopabco.7 $e |- H = { <. x , v >. | ( x e. A /\ v = T ) } $.
    $( Composition of two functions expressed as ordered-pair class
       abstractions.  Note that ` v ` may be assigned to ` w ` , ` y ` , or
       ` z ` if desired.  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 21-Jan-2012.) $)
    fopabco $p |- ( ran F C_ B -> ( G o. F ) = H ) $=
      ( vu crn wss ccom wceq cv wral wcel copab hbopab1 hbxfreq hbrn ax-17 hbss
      cfv wa fveq1i cvv fvopab2 mpan2 syl5eq fveq2d adantl fnopab2 df-f biimpri
      wf wfn ffvelrn sylan wb eleq1d mpbid fvopab4 syl eqtrd cdm dmopab2 eleq2i
      mpan wfun fnfun fvco mp3an12 sylbir 3eqtr4d ex ralrimi fnco hbco eqfnfv2f
      ax-mp sylancl mpbird ) KUBZGUCZLKUDZMUEZAUFZWQUOZWSMUOZUEZAFUGZWPXBAFAUAW
      OGAUAKAUAKWSFUHZBUFHUEUPZABUIZRXEABUAUJUKZULUAUFZGUHAUMUNWPXDXBWPXDUPZWSK
      UOZLUOZJWTXAXIXKHLUOZJXDXKXLUEWPXDXJHLXDXJWSXFUOZHWSKXFRUQXDHURUHXMHUENAB
      FHURUSUTVAZVBVCXIHGUHZXLJUEXIXJGUHZXOWPFGKVGZXDXPKFVHZWPXQABFHKNRVDZXQXRW
      PUPFGKVEVFVTFGWSKVIVJXDXPXOVKWPXDXJHGXNVLVCVMCDHIJGLQSPVNVOVPXDWTXKUEZWPX
      DWSKVQZUHZXTYAFWSABFHKNRVRVSLWAZKWAZYBXTLGVHZYCCDGILOSVDZGLWBWLXRYDXSFKWB
      WLWSLKWCWDWEVCXDXAJUEZWPXDJURUHZYGPXDYHUPXAWSXDEUFJUEUPZAEUIZUOJWSMYJTUQA
      EFJURUSVAUTVCWFWGWHWPWQFVHZMFVHWRXCVKYEXRWPYKYFXSGFLKWIWDAEFJMPTVDAUAFWQM
      AUALKXHLUHAUMXGWJAUAMYJTYIAEUAUJUKWKWMWN $.
      $( [21-Jan-2012] $) $( [21-Aug-2007] $)
  $}

  ${
    $d x y z A $.  $d w x y z B $.  $d w y z C $.  $d w y z D $.  $d w z F $.
    $d w z G $.
    fopabcos.1 $e |- C e. _V $.
    fopabcos.2 $e |- D e. _V $.
    fopabcos.3 $e |- F = { <. x , y >. | ( x e. A /\ y = C ) } $.
    fopabcos.4 $e |- G = { <. x , y >. | ( x e. B /\ y = D ) } $.
    $( MAY BE REVISED - prove from fopabco4? $)
    $( Composition of two functions expressed as ordered-pair class
       abstractions. $)
    fopabcos $p |- ( ran G C_ A -> ( F o. G ) =
                  { <. x , y >. | ( x e. B /\ y = [_ D / x ]_ C ) } ) $=
      ( vz cv wcel csb wceq cfv adantl wfn vw crn wss wa copab fvopab4s fnopab2
      ccom wral fnfvelrn mpan wi ssel adantr mpd eqeltrrd syl cdm dmopab2 fnfun
      eleq2i wfun ax-mp fvco mp3an12 sylbir fveq2d eqtrd csbex eqid cvv wal vex
      ax-gen csbnest1g mp2an syl6eq 3eqtr4d ralrimiva wb ax-17 eqfnfv2f sylancl
      fnco mpbird ) HUBZCUCZGHUHZANDOBNAFEPZQUDABUEZQZMNZWHRZWLWJRZQZMDUIZWGWOM
      DWGWLDOZUDZAWLFPZGRZAWSEPZWMWNWRWSCOWTXAQWRWLHRZWSCWQXBWSQWGABWLFDHJLUFZS
      WRXBWFOZXBCOZWQXDWGHDTZWQXDABDFHJLUGZDWLHUJUKSWGXDXEULWQWFCXBUMUNUOUPABWS
      ECGIKUFUQWQWMWTQWGWQWMXBGRZWTWQWLHURZOZWMXHQZXIDWLABDFHJLUSVAGVBZHVBZXJXK
      GCTZXLABCEGIKUGZCGUTVCXFXMXGDHUTVCWLGHVDVEVFWQXBWSGXCVGVHSWQWNXAQWGWQWNAW
      LWIPZXAABWLWIDWJAFEJIVIZWJVJZUFWLVKOFVKOZAVLXPXAQMVMXSAJVNAWLFEVKVKVOVPVQ
      SVRVSWGWHDTZWJDTWKWPVTXNXFWGXTXOXGCDGHWDVEABDWIWJXQXRUGMUADWHWJUANZWHOMWA
      YAWJOMWAWBWCWE $.
      $( [22-May-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    fsn.1 $e |- A e. _V $.
    fsn.2 $e |- B e. _V $.
    $( A function maps a singleton to a singleton iff it is the singleton of a
       ordered pair. $)
    fsn $p |- ( F : { A } --> { B } <-> F = { <. A , B >. } ) $=
      ( vx vy csn cop wceq cv wcel wb wal wa vex elsn weu eleq1d wrel wreu snid
      wf opelf anbi12i sylib feu mpan2 eueq1 biantru euanv opeq2 pm5.32i anbi1i
      ancom 3bitr4ri df-reu bitr4i 3bitr2i sylibr opeq12 syl5ibrcom impbid opex
      eubii elsnc opth bitr2i syl6bb alrimivv frel relsnop eqrel sylancl mpbird
      ex wf1o f1osn f1oeq1 mpbiri f1of syl impbii ) AHZBHZCUCZCABIZHZJZWFWIFKZG
      KZIZCLZWLWHLZMZGNFNZWFWOFGWFWMWJAJZWKBJZOZWNWFWMWSWFWMWSWFWMOWJWDLZWKWELZ
      OWSWDWEWJWKCGPZUDWTWQXAWRFAQGBQZUEUFVPWFWMWSWGCLZWFAWKIZCLZGWEUAZXDWFAWDL
      XGADUBGWDWEACUGUHXDXDWRGRZOXDWROZGRZXGXHXDGBEUIUJXDWRGUKXJXAXFOZGRXGXIXKG
      WRXFOWRXDOXKXIWRXFXDWRXEWGCWKBAULSUMXAWRXFXCUNXDWRUOUPVEXFGWEUQURUSUTWSWL
      WGCWJWKABVASVBVCWNWLWGJWSWLWGWJWKVDVFWJWKABFPXBEVGVHVIVJWFCTWHTWIWPMWDWEC
      VKABDVLFGCWHVMVNVOWIWDWECVQZWFWIXLWDWEWHVQABDEVRWDWECWHVSVTWDWECWAWBWC $.
      $( [10-Dec-2003] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d F a b $.
    $( A function maps a singleton to a singleton iff it is the singleton of an
       ordered pair. $)
    fsng $p |- ( ( A e. C /\ B e. D ) ->
                 ( F : { A } --> { B } <-> F = { <. A , B >. } ) ) $=
      ( va vb cv csn wf cop wceq wb sneq feq2d opeq1 sneqd eqeq2d bibi12d vex
      feq3 syl opeq2 fsn vtocl2g ) FHZIZGHZIZEJZEUFUHKZIZLZMAIZUIEJZEAUHKZIZLZM
      UNBIZEJZEABKZIZLZMFGABCDUFALZUJUOUMURVDUGUNUIEUFANOVDULUQEVDUKUPUFAUHPQRS
      UHBLZUOUTURVCVEUIUSLUOUTMUHBNUIUSUNEUAUBVEUQVBEVEUPVAUHBAUCQRSUFUHEFTGTUD
      UE $.
      $( [26-Oct-2012] $)
  $}

  ${
    fsn2.1 $e |- A e. _V $.
    $( A function that maps a singleton to a class is the singleton of an
       ordered pair. $)
    fsn2 $p |- ( F : { A } --> B <->
               ( ( F ` A ) e. B /\ F = { <. A , ( F ` A ) >. } ) ) $=
      ( csn wf cfv wcel cop wceq snid ffvelrn mpan2 wfn ffn crn dffn3 cima syl
      wa biimpi wb cdm imadmrn fndm imaeq2d syl5eqr fnsnfv eqtr4d mpbid jca wss
      feq3 snssi fss ancoms sylan impbii fvex fsn anbi2i bitri ) AEZBCFZACGZBHZ
      VCVEEZCFZTZVFCAVEIEJZTVDVIVDVFVHVDAVCHZVFADKZVCBACLMVDCVCNZVHVCBCOVMVCCPZ
      CFZVHVMVOVCCQUAVMVNVGJVOVHUBVMVNCVCRZVGVMVNCCUCZRVPCUDVMVQVCCVCCUEUFUGVMV
      KVGVPJVLVCACUHMUIVNVGVCCUMSUJSUKVFVGBULZVHVDVEBUNVHVRVDVCVGBCUOUPUQURVHVJ
      VFAVECDACUSUTVAVB $.
      $( [19-May-2004] $)
  $}

  ${
    xpsn.1 $e |- A e. _V $.
    xpsn.2 $e |- B e. _V $.
    $( The cross product of two singletons. $)
    xpsn $p |- ( { A } X. { B } ) = { <. A , B >. } $=
      ( csn cxp wf cop wceq fconst fsn mpbi ) AEZBEZMNFZGOABHEIMBDJABOCDKL $.
      $( [4-Nov-2006] $)
  $}

  $( If ` A ` is not in ` C ` , then the restriction of a singleton of
     ` <. A , B >. ` to ` C ` is null.  (Contributed by Scott Fenton,
     15-Apr-2011.) $)
  ressnop0 $p |- ( -. A e. C -> ( { <. A , B >. } |` C ) = (/) ) $=
    ( wcel wn cop cvv cxp csn cres c0 wceq opelxp1 con3i cin df-res incom eqtri
    disjsn biimpri syl5eq syl ) ACDZEABFZCGHZDZEZUDIZCJZKLUFUCABCGMNUGUIUEUHOZK
    UIUHUEOUJUHCPUHUEQRUJKLUGUEUDSTUAUB $.
    $( [1-Oct-2013] $) $( [15-Apr-2011] $)

  ${
    $d A z $.  $d X z $.
    ressnop0OLD.1 $e |- X e. _V $.
    ressnop0OLD.2 $e |- Y e. _V $.
    $( If ` X ` is not in ` A ` , then the restriction of a singleton of
       ` <. X , Y >. ` to ` A ` is null.  (Contributed by Scott Fenton,
       15-Apr-2011.) $)
    ressnop0OLD $p |- ( -. X e. A -> ( { <. X , Y >. } |` A ) = (/) ) $=
      ( vz wcel wn cop csn cres cin cvv cxp c0 df-res xpsn ineq1i wceq sylibr
      inxp 3eqtr2i wo cv wal wa elin elsni eleq1d biimpa sylbi alrimiv eq0 orcd
      con3i xpeq0 syl5eq ) BAGZHZBCIJZAKZBJZALZCJZMLZNZOVAUTAMNZLVBVDNZVGLVFUTA
      PVHUTVGBCDEQRVBVDAMUAUBUSVCOSZVEOSZUCVFOSUSVIVJUSFUDZVCGZHZFUEVIUSVMFVLUR
      VLVKVBGZVKAGZUFURVKVBAUGVNVOURVNVKBAVKBUHUIUJUKUOULFVCUMTUNVCVEUPTUQ $.
      $( [15-Apr-2011] $)
  $}

  ${
    fpr.1 $e |- A e. _V $.
    fpr.2 $e |- B e. _V $.
    fpr.3 $e |- C e. _V $.
    fpr.4 $e |- D e. _V $.
    $( A function with a domain of two elements.  (Contributed by Jeff Madsen,
       20-Jun-2010.)  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    fpr $p |- ( A =/= B
                -> { <. A , C >. , <. B , D >. } : { A , B } --> { C , D } ) $=
      ( wne cop cpr wfn crn wa jctir sylibr csn cun df-pr rnsnop wss wfun funpr
      wf cdm wceq dmprop df-fn rneqi rnun uneq12i eqtr4i 3eqtri eqimssi df-f )
      ABIZACJZBDJZKZABKZLZUSMZCDKZUAZNUTVCUSUDUPVAVDUPUSUBZUSUEUTUFZNVAUPVEVFAB
      CDEFUCABCDUGOUSUTUHPVBVCVBUQQZURQZRZMVGMZVHMZRZVCUSVIUQURSUIVGVHUJVLCQZDQ
      ZRVCVJVMVKVNACEGTBDFHTUKCDSULUMUNOUTVCUSUOP $.
      $( [22-Oct-2011] $) $( [20-Jun-2010] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( A function restricted to a singleton. $)
    fnressn $p |- ( ( F Fn A /\ B e. A ) ->
                  ( F |` { B } ) = { <. B , ( F ` B ) >. } ) $=
      ( vx wcel wfn csn cres cfv cop wceq cv wi sneq reseq2d fveq2 opeq12 mpdan
      wa cvv sneqd eqeq12d imbi2d wss vex snss fnssres sylan2b wf fsn2 biantrur
      dffn2 fvres ax-mp opeq2i sneqi eqeq2i bitr3i 3bitri expcom vtoclga impcom
      fvex snid sylib ) BAECAFZCBGZHZBBCIZJZGZKZVFCDLZGZHZVMVMCIZJZGZKZMVFVLMDB
      AVMBKZVSVLVFVTVOVHVRVKVTVNVGCVMBNOVTVQVJVTVPVIKVQVJKVMBCPVMVPBVIQRUAUBUCV
      FVMAEZVSVFWASVOVNFZVSWAVFVNAUDWBVMADUEZUFAVNCUGUHWBVNTVOUIVMVOIZTEZVOVMWD
      JZGZKZSZVSVNVOULVMTVOWCUJWIWHVSWEWHVMVOVCUKWGVRVOWFVQWDVPVMVMVNEWDVPKVMWC
      VDVMVNCUMUNUOUPUQURUSVEUTVAVB $.
      $( [9-Oct-2004] $)

    $( The value of a function restricted to a singleton. $)
    fressnfv $p |- ( ( F Fn A /\ B e. A ) ->
                 ( ( F |` { B } ) : { B } --> C <-> ( F ` B ) e. C ) ) $=
      ( vx wcel wfn csn cres wf cfv wb cv wi wceq sneq reseq2 syl wa cop eleq1d
      feq1d feq2 bitrd fveq2 bibi12d imbi2d fnressn vex snid fvres ax-mp opeq2i
      sneqi eqeq2i fsn2 eleq1i iba syl5rbbr syl5bb sylbir expcom vtoclga impcom
      ) BAFDAGZBHZCDVFIZJZBDKZCFZLZVEEMZHZCDVMIZJZVLDKZCFZLZNVEVKNEBAVLBOZVRVKV
      EVSVOVHVQVJVSVMVFOZVOVHLVLBPVTVOVMCVGJVHVTVMCVNVGVMVFDQUBVMVFCVGUCUDRVSVP
      VICVLBDUEUAUFUGVEVLAFZVRVEWASVNVLVPTZHZOZVRAVLDUHWDVNVLVLVNKZTZHZOZVRWGWC
      VNWFWBWEVPVLVLVMFWEVPOVLEUIZUJVLVMDUKULZUMUNUOVOWECFZWHSZWHVQVLCVNWIUPVQW
      KWHWLWEVPCWJUQWHWKURUSUTVARVBVCVD $.
      $( [9-Oct-2004] $)
  $}

  $( The value of a constant function. $)
  fvconst $p |- ( ( F : A --> { B } /\ C e. A ) -> ( F ` C ) = B ) $=
    ( csn wf wcel wa cfv wceq ffvelrn elsni syl ) ABEZDFCAGHCDIZNGOBJANCDKOBLM
    $.
    $( [30-May-1999] $)

  ${
    $d x y z A $.  $d x y z B $.
    fopabsn.1 $e |- A e. _V $.
    fopabsn.2 $e |- B e. _V $.
    $( The singleton of an ordered pair expressed as an ordered pair class
       abstraction.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    fopabsn $p |- { <. A , B >. } =
                  { <. x , y >. | ( x e. { A } /\ y = B ) } $=
      ( csn cxp cop cv wcel wceq wa copab xpsn fconstopab eqtr3i ) CGZDGHCDIGAJ
      RKBJDLMABNCDEFOABRDPQ $.
      $( [22-Oct-2011] $) $( [6-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    fopabap.0a $e |- A e. _V $.
    fopabap.0b $e |- B e. _V $.
    fopabap.1 $e |- ( R u. { A } ) = S $.
    fopabap.2 $e |- ( x = A -> C = B ) $.
    $( Append an additional value to a function. $)
    fopabap $p |- ( { <. x , y >. | ( x e. R /\ y = C ) } u. { <. A , B >. } )
                  = { <. x , y >. | ( x e. S /\ y = C ) } $=
      ( cv wcel wceq wa copab cop csn cun wo uneq2i unopab eleq2i bitr3i anbi1i
      fopabsn elun andir sylbi eqeq2d pm5.32i orbi2i 3bitrri opabbii 3eqtri
      elsn ) ALZFMZBLZENZOZABPZCDQRZSVBUQCRZMZUSDNZOZABPZSVAVGTZABPUQGMZUTOZABP
      VCVHVBABCDHIUFUAVAVGABUBVIVKABVKURVETZUTOVAVEUTOZTVIVJVLUTVJUQFVDSZMVLVNG
      UQJUCUQFVDUGUDUEURVEUTUHVMVGVAVEUTVFVEEDUSVEUQCNEDNACUPKUIUJUKULUMUNUO $.
      $( [15-Jun-2007] $) $( [6-Jun-2006] $)
  $}

  ${
    $d x A $.
    $( The value of the identity function. $)
    fvi $p |- ( A e. V -> ( _I ` A ) = A ) $=
      ( vx cv cid cfv wceq fveq2 id eqeq12d cvv wfn wcel wfun cdm funi mpbir2an
      df-fn dmi vex cop wbr ididg ax-mp df-br mpbi fnopfvb mpbiri mp2an vtoclg
      wa ) CDZEFZULGZAEFZAGCABULAGZUMUOULAULAEHUPIJEKLZULKMZUNUQENEOKGEKRPSQCTZ
      UQURUKUNULULUAEMZULULEUBZUTURVAUSULKUCUDULULEUEUFKULULEUSUGUHUIUJ $.
      $( [1-May-2004] $)
  $}

  $( The value of a restricted identity function. $)
  fvresi $p |- ( B e. A -> ( ( _I |` A ) ` B ) = B ) $=
    ( wcel cid cres cfv fvres fvi eqtrd ) BACBDAEFBDFBBADGBAHI $.
    $( [19-May-2004] $)

  $( Remove an ordered pair not participating in a function value.  (Revised by
     Mario Carneiro, 28-May-2014.) $)
  fvunsn $p |- ( B =/= D
       -> ( ( A u. { <. B , C >. } ) ` D ) = ( A ` D ) ) $=
    ( wne cop csn cun cres cfv c0 wcel wceq syl cvv fvres fvprc eqtr4d pm2.61i
    wn resundir elsni necon3ai ressnop0 uneq2d un0 syl6eq syl5eq fveq1d 3eqtr3g
    snidg ) BDEZDABCFGZHZDGZIZJZDAUOIZJZDUNJZDAJZULDUPURULUPURUMUOIZHZURAUMUOUA
    ULVCURKHURULVBKURULBUOLZTVBKMVDBDBDUBUCBCUOUDNUEURUFUGUHUIDOLZUQUTMZVEDUOLZ
    VFDOUKZDUOUNPNVETZUQKUTDUPQDUNQRSVEUSVAMZVEVGVJVHDUOAPNVIUSKVADURQDAQRSUJ
    $.
    $( [28-May-2014] $) $( [1-Oct-2013] $)

  ${
    fvsn.1 $e |- A e. _V $.
    fvsn.2 $e |- B e. _V $.
    $( The value of a singleton of an ordered pair is the second member. $)
    fvsn $p |- ( { <. A , B >. } ` A ) = B $=
      ( cop csn wfun wcel cfv wceq funsn opex snid funopfv mp2 ) ABEZFZGPQHAQIB
      JABCKPABLMABQDNO $.
      $( [12-Aug-1994] $)
  $}

  ${
    $d A a b $.  $d B b $.
    $( The value of a singleton of an ordered pair is the second member. $)
    fvsng $p |- ( ( A e. V /\ B e. W ) -> ( { <. A , B >. } ` A ) = B ) $=
      ( va vb cv cop csn cfv opeq1 sneqd id fveq12d eqeq1d opeq2 fveq1d eqeq12d
      wceq vex fvsn vtocl2g ) EGZUCFGZHZIZJZUDSAAUDHZIZJZUDSAABHZIZJZBSEFABCDUC
      ASZUGUJUDUNUCAUFUIUNUEUHUCAUDKLUNMNOUDBSZUJUMUDBUOAUIULUOUHUKUDBAPLQUOMRU
      CUDETFTUAUB $.
      $( [26-Oct-2012] $)
  $}

  ${
    fvsnun.1 $e |- A e. _V $.
    fvsnun.2 $e |- B e. _V $.
    fvsnun.3 $e |- G = ( { <. A , B >. } u. ( F |` ( C \ { A } ) ) ) $.
    $( The value of a function with one of its ordered pairs replaced, at the
       replaced ordered pair.  See also ~ fvsnun2 . $)
    fvsnun1 $p |- ( G ` A ) = B $=
      ( csn cres cfv wcel wceq fvres ax-mp cun c0 cin eqtri 3eqtri cop resundir
      snid cdif reseq1 incom disjdif resdisj uneq2i un0 fveq1i fvsn eqtr3i ) AE
      AIZJZKZAEKZBAUNLZUPUQMAFUCZAUNENOUPAABUAIZUNJZKZAUTKZBAUOVAUOUTDCUNUDZJZP
      ZUNJZVAVEUNJZPZVAEVFMUOVGMHEVFUNUEOUTVEUNUBVIVAQPVAVHQVAVDUNRZQMVHQMVJUNV
      DRQVDUNUFUNCUGSVDUNDUHOUIVAUJSTUKURVBVCMUSAUNUTNOABFGULTUM $.
      $( [23-Sep-2007] $)

    $( The value of a function with one of its ordered pairs replaced, at
       arguments other than the replaced one.  See also ~ fvsnun1 . $)
    fvsnun2 $p |- ( D e. ( C \ { A } ) -> ( G ` D ) = ( F ` D ) ) $=
      ( csn cdif wcel cres cfv fvres cun wceq ax-mp c0 3eqtri cop reseq1 cin wb
      resundir disjdif wfn fnsn fnresdisj mpbi residm uneq12i un0 fveq1i syl5eq
      uncom eqtr3d ) DCAJZKZLZDFUSMZNZDFNDENZDUSFOUTVBDEUSMZNVCDVAVDVAABUAJZVDP
      ZUSMZVEUSMZVDUSMZPZVDFVFQVAVGQIFVFUSUBRVEVDUSUEVJSVDPVDSPVDVHSVIVDURUSUCS
      QZVHSQZURCUFVEURUGVKVLUDABGUHURUSVEUIRUJEUSUKULSVDUPVDUMTTUNDUSEOUOUQ $.
      $( [23-Sep-2007] $)
  $}

  ${
    fvpr1.1 $e |- A e. _V $.
    fvpr1.2 $e |- C e. _V $.
    $( The value of a function with a domain of two elements.  (Contributed by
       Jeff Madsen, 20-Jun-2010.) $)
    fvpr1 $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` A ) = C ) $=
      ( wne cop cpr cfv csn df-pr fveq1i wceq necom fvunsn sylbi syl5eq fvsn
      cun syl6eq ) ABGZAACHZBDHZIZJZAUCKZJZCUBUFAUGUDKTZJZUHAUEUIUCUDLMUBBAGUJU
      HNABOUGBDAPQRACEFSUA $.
      $( [1-Oct-2013] $) $( [20-Jun-2010] $)
  $}

  ${
    fvpr2.1 $e |- B e. _V $.
    fvpr2.2 $e |- D e. _V $.
    $( The value of a function with a domain of two elements.  (Contributed by
       Jeff Madsen, 20-Jun-2010.) $)
    fvpr2 $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` B ) = D ) $=
      ( wne cop cpr cfv wceq fvpr1 necom prcom fveq1i eqeq1i 3imtr4i ) BAGBBDHZ
      ACHZIZJZDKABGBSRIZJZDKBADCEFLABMUCUADBUBTSRNOPQ $.
      $( [1-Oct-2013] $) $( [20-Jun-2010] $)
  $}

  ${
    fvpr1OLD.1 $e |- A e. _V $.
    fvpr1OLD.2 $e |- B e. _V $.
    fvpr1OLD.3 $e |- C e. _V $.
    fvpr1OLD.4 $e |- D e. _V $.
    $( The value of a function with a domain of two elements.  (Contributed by
       Jeff Madsen, 20-Jun-2010.) $)
    fvpr1OLD $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` A ) = C ) $=
      ( fvpr1 ) ABCDEGI $.
      $( [3-Sep-2011] $) $( [20-Jun-2010] $)

    $( The value of a function with a domain of two elements.  (Contributed by
       Jeff Madsen, 20-Jun-2010.) $)
    fvpr2OLD $p |- ( A =/= B -> ( { <. A , C >. , <. B , D >. } ` B ) = D ) $=
      ( fvpr2 ) ABCDFHI $.
      $( [3-Sep-2011] $) $( [20-Jun-2010] $)
  $}

  ${
    fvtp1.1 $e |- A e. _V $.
    fvtp1.4 $e |- D e. _V $.
    $( The first value of a function with a domain of three elements. $)
    fvtp1 $p |- ( ( A =/= B /\ A =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` A ) = D ) $=
      ( wne wa cop ctp cfv cpr csn cun df-tp fveq1i wceq necom fvunsn sylan9eqr
      sylbi fvpr1 syl5eq ) ABIZACIZJAADKZBEKZCFKZLZMAUHUINZUJOPZMZDAUKUMUHUIUJQ
      RUGUFUNAULMZDUGCAIUNUOSACTULCFAUAUCABDEGHUDUBUE $.
      $( [1-Oct-2013] $) $( [14-Sep-2011] $)
  $}

  ${
    fvtp2.1 $e |- B e. _V $.
    fvtp2.4 $e |- E e. _V $.
    $( The second value of a function with a domain of three elements. $)
    fvtp2 $p |- ( ( A =/= B /\ B =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` B ) = E ) $=
      ( wne wa cop ctp cfv tprot fveq1i wceq necom fvtp1 ancoms sylanb syl5eq )
      ABIZBCIZJBADKZBEKZCFKZLZMBUEUFUDLZMZEBUGUHUDUEUFNOUBBAIZUCUIEPZABQUCUJUKB
      CAEFDGHRSTUA $.
      $( [1-Oct-2013] $) $( [14-Sep-2011] $)
  $}

  ${
    fvtp3.1 $e |- C e. _V $.
    fvtp3.4 $e |- F e. _V $.
    $( The third value of a function with a domain of three elements. $)
    fvtp3 $p |- ( ( A =/= C /\ B =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` C ) = F ) $=
      ( wne wa cop ctp cfv tprot fveq1i wceq necom fvtp2 sylan2b ancoms syl5eq
      ) ACIZBCIZJCADKZBEKZCFKZLZMCUEUFUDLZMZFCUGUHUDUEUFNOUCUBUIFPZUBUCCAIUJACQ
      BCAEFDGHRSTUA $.
      $( [1-Oct-2013] $) $( [14-Sep-2011] $)
  $}

  ${
    fvtp1OLD.1 $e |- A e. _V $.
    fvtp1OLD.2 $e |- B e. _V $.
    fvtp1OLD.3 $e |- C e. _V $.
    fvtp1OLD.4 $e |- D e. _V $.
    fvtp1OLD.5 $e |- E e. _V $.
    fvtp1OLD.6 $e |- F e. _V $.
    $( The first value of a function with a domain of three elements. $)
    fvtp1OLD $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` A ) = D ) $=
      ( wne w3a cop ctp wfun cfv wceq funtp wcel opex tpid1 funopfv mpi syl ) A
      BMACMBCMNADOZBEOZCFOZPZQZAUJRDSZABCDEFGHITUKUGUJUAULUGUHUIADUBUCADUJJUDUE
      UF $.
      $( [14-Sep-2011] $)

    $( The second value of a function with a domain of three elements. $)
    fvtp2OLD $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` B ) = E ) $=
      ( wne w3a cop ctp wfun cfv wceq funtp wcel opex tpid2 funopfv mpi syl ) A
      BMACMBCMNADOZBEOZCFOZPZQZBUJRESZABCDEFGHITUKUHUJUAULUGUHUIBEUBUCBEUJKUDUE
      UF $.
      $( [14-Sep-2011] $)

    $( The third value of a function with a domain of three elements. $)
    fvtp3OLD $p |- ( ( A =/= B /\ A =/= C /\ B =/= C )
             -> ( { <. A , D >. , <. B , E >. , <. C , F >. } ` C ) = F ) $=
      ( wne w3a cop ctp wfun cfv wceq funtp wcel opex tpid3 funopfv mpi syl ) A
      BMACMBCMNADOZBEOZCFOZPZQZCUJRFSZABCDEFGHITUKUIUJUAULUGUHUICFUBUCCFUJLUDUE
      UF $.
      $( [14-Sep-2011] $)
  $}

  $( The value of a constant function. $)
  fvconst2g $p |- ( ( B e. D /\ C e. A ) -> ( ( A X. { B } ) ` C ) = B ) $=
    ( wcel csn cxp wf cfv wceq fconstg fvconst sylan ) BDEABFZANGZHCAECOIBJABDK
    ABCOLM $.
    $( [20-Aug-2005] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( A constant function expressed as a cross product. $)
    fconst2g $p |- ( B e. C -> ( F : A --> { B } <-> F = ( A X. { B } ) ) ) $=
      ( vx wcel csn wf cxp wceq wa cv cfv fvconst adantlr fvconst2g adantll wfn
      wral ffn eqtr4d ralrimiva wb fconstg syl eqfnfv2 syl2an mpbird syl5ibrcom
      expcom feq1 impbid ) BCFZABGZDHZDAUNIZJZUOUMUQUOUMKZUQELZDMZUSUPMZJZEASZU
      RVBEAURUSAFZKUTBVAUOVDUTBJUMABUSDNOUMVDVABJUOABUSCPQUAUBUODARUPARZUQVCUCU
      MAUNDTUMAUNUPHZVEABCUDZAUNUPTUEEADUPUFUGUHUJUMUOUQVFVGAUNDUPUKUIUL $.
      $( [27-Nov-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    fvconst2.1 $e |- B e. _V $.
    $( The value of a constant function. $)
    fvconst2 $p |- ( C e. A -> ( ( A X. { B } ) ` C ) = B ) $=
      ( cvv wcel csn cxp cfv wceq fvconst2g mpan ) BEFCAFCABGHIBJDABCEKL $.
      $( [16-Apr-2005] $)

    $( A constant function expressed as a cross product. $)
    fconst2 $p |- ( F : A --> { B } <-> F = ( A X. { B } ) ) $=
      ( cvv wcel csn wf cxp wceq wb fconst2g ax-mp ) BEFABGZCHCANIJKDABECLM $.
      $( [20-Aug-1999] $)
  $}

  $( Two ways to express that a function is constant. $)
  fconst5 $p |- ( ( F Fn A /\ A =/= (/) ) -> ( F = ( A X. { B } ) <->
                 ran F = { B } ) ) $=
    ( wfn c0 wne wa csn cxp wceq crn wi eqeq2d syl5ib adantl cvv adantrd adantr
    rneq wb rnxp wcel wf wfo df-fo fof sylbir fconst2g exp3a fnrel snprc relrn0
    wn wrel biimprd eqeq2 xpeq2 xp0 syl6eq 3imtr4d ex sylbi syl5 pm2.61i impbid
    ) CADZAEFZGZCABHZIZJZCKZVIJZVGVKVMLVFVKVLVJKZJVGVMCVJSVGVNVIVLAVIUAMNOBPUBZ
    VHVMVKLZLVOVFVPVGVOVFVMVKVFVMGZAVICUCZVOVKVQAVICUDVRAVICUEAVICUFUGABPCUHNUI
    QVOUMZVFVPVGVFCUNZVSVPACUJVSVIEJZVTVPLBUKWAVTVPWAVTGVLEJZCEJZVMVKVTWBWCLWAV
    TWCWBCULUOOWAVMWBTVTVIEVLUPRWAVKWCTVTWAVJECWAVJAEIEVIEAUQAURUSMRUTVAVBVCQVD
    VE $.
    $( [27-Nov-2007] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( A constant function expressed in terms of its functionality, domain, and
       value.  See also ~ fconst2 . $)
    fconstfv $p |- ( F : A --> { B } <->
                   ( F Fn A /\ A. x e. A ( F ` x ) = B ) ) $=
      ( vy vz csn wf wfn cv cfv wceq wral wa syl6bb wcel wrex eqeq1d sylan9bbr
      c0 ffn fvconst ralrimiva jca wi fneq2 fn0 feq1 mpbiri syl6bi feq2 sylibrd
      f0 adantrd wne fvelrnb fveq2 rcla4cva rexbidva r19.9rzv bicomd elsn eqcom
      crn bitr2i eqrdv an32s exp31 imdistand df-fo sylbir syl6 pm2.61ine impbii
      wfo fof ) BCGZDHZDBIZAJZDKZCLZABMZNZVRVSWCBVQDUAVRWBABBCVTDUBUCUDWDVRUEBT
      BTLZVSVRWCWEVSTVQDHZVRWEVSDTLZWFWEVSDTIWGBTDUFDUGOWGWFTVQTHVQUMTVQDTUHUIU
      JBTVQDUKULUNBTUOZWDVSDVDZVQLZNZVRWHVSWCWJWHVSWCWJWHWCVSWJWHWCNZVSNZEWIVQW
      MEJZWIPZCWNLZWNVQPZVSWOFJZDKZWNLZFBQZWLWPFBWNDUPWCXAWPFBQZWHWPWCWTWPFBWCW
      RBPNWSCWNWBWSCLAWRBVTWRLWAWSCVTWRDUQRURRUSWHWPXBWPFBUTVASSWQWNCLWPECVBWNC
      VCVEOVFVGVHVIWKBVQDVOVRBVQDVJBVQDVPVKVLVMVN $.
      $( [27-Aug-2004] $)

    $( Two ways to express a constant function. $)
    fconst3 $p |- ( F : A --> { B } <->
                  ( F Fn A /\ A C_ ( `' F " { B } ) ) ) $=
      ( vx csn wf wfn cv cfv wceq wral wa ccnv cima wss fconstfv wfun cdm fnfun
      wb fndm eqimss2 syl funconstss syl2anc pm5.32i bitri ) ABEZCFCAGZDHCIBJDA
      KZLUIACMUHNOZLDABCPUIUJUKUICQACRZOZUJUKTACSUIULAJUMACUAAULUBUCDABCUDUEUFU
      G $.
      $( [15-Mar-2007] $)
  $}

  $( Two ways to express a constant function. $)
  fconst4 $p |- ( F : A --> { B } <->
                ( F Fn A /\ ( `' F " { B } ) = A ) ) $=
    ( csn wf wfn ccnv cima wss wa wceq fconst3 cnvimass fndm syl5sseq biantrurd
    cdm eqss syl6bbr pm5.32i bitri ) ABDZCECAFZACGUBHZIZJUCUDAKZJABCLUCUEUFUCUE
    UDAIZUEJUFUCUGUEUCCQUDACUBMACNOPUDARSTUA $.
    $( [8-Mar-2007] $)

  $( A function's value in a pre-image belongs to the image. $)
  funfvima $p |- ( ( Fun F /\ B e. dom F ) -> ( B e. A ->
                 ( F ` B ) e. ( F " A ) ) ) $=
    ( wfun cdm wcel wa cfv cima wi cres cin dmres eleq2i elin crn funres fvelrn
    bitri sylan fvres eleq1d syl6rbbr syl5ibrcom ex syl5bir exp3a com12 pm2.43b
    df-ima imp3a ) CDZBCEZFZGBAFZBCHZCAIZFZUOULUNUOURJZULUOUNUSJULUOUNUSUOUNGZB
    CAKZEZFZULUSVCBAUMLZFUTVBVDBCAMNBAUMOSULVCUSULVCGURUOBVAHZVAPZFZULVADVCVGAC
    QBVARTUOVGUPVFFURUOVEUPVFBACUAUBUQVFUPCAUJNUCUDUEUFUGUHUKUI $.
    $( [23-Sep-2003] $)

  $( A function's value in an included pre-image belongs to the image. $)
  funfvima2 $p |- ( ( Fun F /\ A C_ dom F ) -> ( B e. A ->
                  ( F ` B ) e. ( F " A ) ) ) $=
    ( wfun cdm wss wcel cfv cima wi ssel funfvima ex com23 a2d syl5 imp ) CDZAC
    EZFZBAGZBCHCAIGZJZTUABSGZJRUCASBKRUAUDUBRUDUAUBRUDUCABCLMNOPQ $.
    $( [3-Feb-1997] $)

  ${
    $d x A $.  $d x F $.  $d x G $.
    $( A class including a function contains the function's value in the image
       of the singleton of the argument. $)
    funfvima3 $p |- ( ( Fun F /\ F C_ G ) -> ( A e. dom F ->
                    ( F ` A ) e. ( G " { A } ) ) ) $=
      ( vx wss wfun cdm wcel cfv csn cima wi wa cop funfvop ssel syl5 imp wb cv
      wceq sneq imaeq2d eleq2d opeq1 eleq1d vex elimasn vtoclbg ad2antll mpbird
      fvex exp32 impcom ) BCEZBFZABGZHZABIZCAJZKZHZLUOUPURVBUOUPURMZMVBAUSNZCHZ
      UOVCVEVCVDBHUOVEABOBCVDPQRURVBVESUOUPUSCDTZJZKZHVFUSNZCHVBVEDAUQVFAUAZVHV
      AUSVJVGUTCVFAUBUCUDVJVIVDCVFAUSUEUFCVFUSDUGABULUHUIUJUKUMUN $.
      $( [23-Mar-2004] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d B x y $.  $d ph x $.  $d ps y $.
    raleqfn.1 $e |- ( y = ( F ` x ) -> ( ph <-> ps ) ) $.
    $( Change the domain of quantification by a function.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    raleqfn $p |- ( ( F Fn A /\ B C_ A ) ->
                              ( A. x e. B ps <-> A. y e. ( F " B ) ph ) ) $=
      ( wfn wss wa wral cima cv wcel wrex wi biimpar com23 ralrimdv cfv r19.29r
      wceq fvelimab wb eqcoms adantl rexlimiva syl syl6bi wfun cdm fnfun adantr
      ex fndm sseq2d funfvima2 syl2anc rcla4v syl6 impbid ) GEIZFEJZKZBCFLZADGF
      MZLZVEVFADVGVEDNZVGOZVFAVEVJCNZGUAZVIUCZCFPZVFAQCEFVIGUDVNVFAVNVFKVMBKZCF
      PAVMBCFUBVOACFVOAVKFOZVMABABUEVIVLHUFRUGUHUIUOUJSTVEVHBCFVEVPVHBVEVPVLVGO
      ZVHBQVEGUKZFGULZJZVPVQQVCVRVDEGUMUNVCVTVDVCVSEFEGUPUQRFVKGURUSABDVLVGHUTV
      ASTVB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y F $.
    $( Upper bound for the class of values of a class. $)
    fvclss $p |- { y | E. x y = ( F ` x ) } C_ ( ran F u. { (/) } ) $=
      ( cv cfv wceq wex cab crn wcel c0 csn wo cun wn wne wbr eqcom vex syl6ibr
      tz6.12i syl5bi eximdv elrn com12 necon1bd elsn orrd ss2abi df-un sseqtr4i
      ) BDZADZCEZFZAGZBHULCIZJZULKLZJZMZBHUQUSNUPVABUPURUTUPUROULKFUTUPURULKULK
      PZUPURVBUPUMULCQZAGURVBUOVCAUOUNULFVBVCULUNRUMULCASUAUBUCAULCBSUDTUEUFBKU
      GTUHUIBUQUSUJUK $.
      $( [9-Nov-1995] $)
  $}

  ${
    $d x y F $.
    fvclex.1 $e |- F e. _V $.
    $( Existence of the class of values of a set. $)
    fvclex $p |- { y | E. x y = ( F ` x ) } e. _V $=
      ( cv cfv wceq wex cab crn c0 csn cun rnex p0ex unex fvclss ssexi ) BEAECF
      GAHBICJZKLZMSTCDNOPABCQR $.
      $( [9-Nov-1995] $)
  $}

  ${
    $d x y z w F $.  $d x y A $.
    fvresex.1 $e |- A e. _V $.
    $( Existence of the class of values of a restricted class. $)
    fvresex $p |- { y | E. x y = ( ( F |` A ) ` x ) } e. _V $=
      ( vw vz cv cfv wceq copab cres wex cab cvv vex fvex fveq2 fvopab wcel
      fveqres ax-mp eqeq2i exbii abbii wfun funopabeq resfunexg fvclex eqeltrri
      mp2an ) BHZAHZFHGHZDIZJGFKZCLZIZJZAMZBNULUMDCLIZJZAMZBNOUTVCBUSVBAURVAULU
      MUPIUMDIZJURVAJGFUMUOVDAPUMDQUNUMDRSUMCUPDUAUBUCUDUEABUQUPUFCOTUQOTGFUOUG
      EUPCOUHUKUIUJ $.
      $( [14-Nov-1995] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.
    abrexex.1 $e |- A e. _V $.
    $( Lemma for ~ abrexex .  Shows the existence of a class of existentially
       restricted function values. $)
    abrexexlem1 $p |- { z | E. y e. A z = ( B ` y ) } e. _V $=
      ( cv cfv wceq wrex cab cres wex fvresex wcel wa df-rex fvres eqeq2d eximi
      biimpar sylbi ss2abi ssexi ) BFZAFZDGZHZACIZBJUDUEDCKGZHZALZBJABCDEMUHUKB
      UHUECNZUGOZALUKUGACPUMUJAULUJUGULUIUFUDUECDQRTSUAUBUC $.
      $( [22-Apr-2004] $)

    ${
      abrexexlem2.2 $e |- B e. _V $.
      $( Lemma for ~ abrexex .  Almost there, but still requires that ` B ` be
         a set. $)
      abrexexlem2 $p |- { y | E. x e. A y = B } e. _V $=
        ( vw vz cv wceq wrex cab copab cfv cvv wcel eqtri ax-17 hbfv hbeq mp2an
        vex biantrur opabbii fveq1i fvopab2 eqeq2i rexbii hbopab1 eqeq2d cbvrex
        wa fveq2 bitr3i abbii hbopab2 hbrex eqeq1 rexbidv abrexexlem1 eqeltri
        cbvab ) BIZDJZACKZBLZGIZHIZVDABMZNZJZHCKZGLZOVFVCVJJZHCKZBLVMVEVOBVEVCA
        IZVINZJZACKVOVRVDACVQDVCVQVPVPOPZVDULZABMZNZDVPVIWAVDVTABVSVDAUBZUCUDUE
        VSDOPWBDJWCFABODOUFUAQUGUHVRVNAHCVRHRAGGVCVJVGVCPARAGVHVIVDABGUIVGVHPZA
        RSTVPVHJVQVJVCVPVHVIUMUJUKUNUOVOVLBGVOGRVKBHCVHCPBRBAGVGVJVPVGPBRBGVHVI
        VDABGUPWDBRSTUQVCVGJVNVKHCVCVGVJURUSVBQHGCVIEUTVA $.
        $( [16-Oct-2003] $)
    $}

    $( Existence of a class abstraction of existentially restricted sets. ` x `
       is normally a free-variable parameter in the class expression
       substituted for ` B ` , which can be thought of as ` B ( x ) ` .  This
       simple-looking theorem is actually quite powerful and appears to involve
       the Axiom of Replacement in an intrinsic way, as can be seen by tracing
       back through the path ~ abrexexlem2 , ~ abrexexlem1 , ~ fvresex ,
       ~ resfunexg , and ~ funimaexg .  See also ~ abrexex2 . $)
    abrexex $p |- { y | E. x e. A y = B } e. _V $=
      ( vz cv wceq wrex cab cvv wcel crab class2set abrexexlem2 vex eleq1 mpbii
      wb wral ax-1 ralrimiv rabid2 sylibr eqeq2d syl ibi reximi ss2abi ssexi )
      BGZDHZACIZBJUKDKLZFDMZHZACIZBJABCUOEFDNOUMUQBULUPACULUPULUNULUPSULUKKLUNB
      PUKDKQRUNDUOUKUNUNFDTDUOHUNUNFDUNFGDLUAUBUNFDUCUDUEUFUGUHUIUJ $.
      $( [16-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( Existence of a class abstraction of existentially restricted sets. ` x `
       is normally a free-variable parameter in ` B ` .  The antecedent assures
       us that ` A ` is a set. $)
    abrexexg $p |- ( A e. V -> { y | E. x e. A y = B } e. _V ) $=
      ( vz cv wceq wrex cab cvv wcel rexeq abbidv eleq1d vex abrexex vtoclg ) B
      GDHZAFGZIZBJZKLSACIZBJZKLFCETCHZUBUDKUEUAUCBSATCMNOABTDFPQR $.
      $( [3-Nov-2003] $)
  $}

  ${
    $d w y z B $.  $d w x y z A $.
    elabrex.1 $e |- B e. _V $.
    $( Elementhood in an image set.  (Contributed by Mario Carneiro,
       14-Jan-2014.) $)
    elabrex $p |- ( x e. A -> B e. { y | E. x e. A y = B } ) $=
      ( vz vw cv wcel csb wceq wrex cab wtru tru csbeq1a eqcoms a1i 2thd ax-17
      rcla4ev mpan2 rexbidv elab sylibr vex hbcsb1 hbeq eqeq2d cbvrex syl6eleqr
      eqeq1 abbii ) AHZCIZDBHZAFHZDJZKZFCLZBMZUPDKZACLZBMUODURKZFCLZDVAIUONVEOV
      DNFUNCUQUNKZVDNVDUNUQAUQDPZQNVFORSUAUBUTVEBDEVBUSVDFCUPDURULUCUDUEVCUTBVB
      USAFCVBFTAGGUPURGHZUPIATAGUQDFUFVHUQIATUGUHUNUQKDURUPVGUIUJUMUK $.
      $( [14-Jan-2014] $)
  $}

  ${
    $d A y z $.  $d B y z $.  $d C w $.  $d D y $.  $d w x y $.  $d w z y $.
    abrexco.1 $e |- B e. _V $.
    abrexco.2 $e |- ( y = B -> C = D ) $.
    $( Composition of two image maps ` C ( y ) ` and ` B ( w ) ` . $)
    abrexco $p |- { x | E. y e. { z | E. w e. A z = B } x = C } =
        { x | E. w e. A x = D } $=
      ( cv wceq wrex cab wa wex wcel df-rex vex exbii eqeq1 rexbidv elab anbi1i
      r19.41v bitr4i bitri rexcom4 19.41v eqeq2d pm5.32i isseti biantrur rexbii
      3bitr4i 3bitr2i abbii ) AKZGLZBCKZFLZDEMZCNZMZURHLZDEMZAVDBKZFLZUSOZDEMZB
      PZVIBPZDEMVFVDVGVCQZUSOZBPVKUSBVCRVNVJBVNVHDEMZUSOVJVMVOUSVBVOCVGBSUTVGLV
      AVHDEUTVGFUAUBUCUDVHUSDEUEUFTUGVIDBEUHVLVEDEVHVEOZBPVHBPZVEOVLVEVHVEBUIVI
      VPBVHUSVEVHGHURJUJUKTVQVEBFIULUMUOUNUPUQ $.
      $( [27-May-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The existence of an indexed union. ` x ` is normally a free-variable
       parameter in ` B ` . $)
    iunexg $p |- ( ( A e. V /\ A. x e. A B e. W ) -> U_ x e. A B e. _V ) $=
      ( vy wcel wral ciun wceq wrex cab cuni cvv dfiun2g adantl abrexexg uniexg
      wa cv syl adantr eqeltrd ) BDGZCEGABHZSABCIZFTCJABKFLZMZNUEUFUHJUDAFBCEOP
      UDUHNGZUEUDUGNGUIAFBCDQUGNRUAUBUC $.
      $( [23-Mar-2006] $)
  $}

  ${
    $d A x y z $.  $d V x y z $.  $d W x y z $.  $d ph z $.
    $( Existence of an existentially restricted class abstraction.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    abrexex2g $p |- ( ( A e. V /\ A. x e. A { y | ph } e. W )
                                  -> { y | E. x e. A ph } e. _V ) $=
      ( vz wcel cab wral wa wrex cv cvv wsbc ax-17 hbs1 hbrex wceq sbceq1a ciun
      rexbidv cbvab df-clab rexbii eqtr4i df-iun iunexg syl5eqelr syl5eqel
      abbii ) DEHACIZFHBDJKZABDLZCIZGMZULHZBDLZGIZNUOACUPOZBDLZGIUSUNVACGUNGPUT
      CBDBMDHCPACGQRCMUPSAUTBDACUPTUBUCURVAGUQUTBDAGCUDUEUKUFUMUSBDULUANBGDULUG
      BDULEFUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y v w z $.  $d ph v w z $.
    opabex3.1 $e |- A e. _V $.
    opabex3.2 $e |- ( x e. A -> { y | ph } e. _V ) $.
    $( Existence of an ordered pair abstraction.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    opabex3 $p |- { <. x , y >. | ( x e. A /\ ph ) } e. _V $=
      ( vz vv vw cv wcel wa cvv wex cop wceq an12 exbii bitri eqeq2d ciun copab
      csn cab cxp 19.42v elxp excom anbi1i vex opeq1 anbi1d ceqsexv ax-17 hbab1
      elsn hban opeq2 wsbc sbceq1a eqcoms df-clab syl6rbbr anbi12d cbvex 3bitri
      wb anbi2i 3bitr4ri wrex eliun df-rex elopab 3bitr4i eqriv wral snex xpexg
      sylancr rgen iunexg mp2an eqeltrri ) BDBJZUCZACUDZUEZUAZWDDKZALZBCUBZMGWH
      WKWIGJZWGKZLZBNZWLWDCJZOZPZWJLZCNZBNWLWHKZWLWKKWNWTBWIWRALZLZCNWIXBCNZLWT
      WNWIXBCUFWSXCCWRWIAQRWMXDWIWMWLHJZIJZOZPZXEWEKZXFWFKZLLZINHNZWLWDXFOZPZXJ
      LZINZXDHIWLWEWFUGXLXKHNZINXPXKHIUHXQXOIXQXEWDPZXHXJLZLZHNXOXKXTHXKXIXSLXT
      XHXIXJQXIXRXSHWDUPUISRXSXOHWDBUJXRXHXNXJXRXGXMWLXEWDXFUKTULUMSRSXOXBICXNX
      JCXNCUNACIUOUQXBIUNXFWPPZXNWRXJAYAXMWQWLXFWPWDURTYAAACXFUSZXJAYBVGWPXFACX
      FUTVAAICVBVCVDVEVFVHVIRXAWMBDVJWOBWLDWGVKWMBDVLSWJBCWLVMVNVODMKWGMKZBDVPW
      HMKEYCBDWIWEMKWFMKYCWDVQFWEWFMMVRVSVTBDWGMMWAWBWC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x A $.
    iunex.1 $e |- A e. _V $.
    iunex.2 $e |- B e. _V $.
    $( The existence of an indexed union. ` x ` is normally a free-variable
       parameter in the class expression substituted for ` B ` , which can be
       read informally as ` B ( x ) ` . $)
    iunex $p |- U_ x e. A B e. _V $=
      ( cvv wcel wral ciun cv a1i rgen iunexg mp2an ) BFGCFGZABHABCIFGDOABOAJBG
      EKLABCFFMN $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The image of a union is the indexed union of the images.  Theorem 3K(a)
       of [Enderton] p. 50. $)
    imaiun $p |- ( A " U. B ) = U_ x e. B ( A " x ) $=
      ( vy vz cuni cima cv ciun wcel cop wa wex wrex eluni anbi1i 19.41v 3bitri
      exbii elima3 anass an12 bitri bitr3i excom vex rexbii df-rex bitr2i eliun
      exdistr 3bitr4i eqriv ) DBCFZGZACBAHZGZIZEHZUNJZUSDHZKBJZLZEMZVAUQJZACNZV
      AUOJVAURJVDUSUPJZUPCJZLZAMZVBLZEMZVHVGVBLZEMZLAMZVFVCVKEUTVJVBAUSCOPSVLVH
      VMLZAMZEMVPEMAMVOVKVQEVKVIVBLZAMVQVIVBAQVRVPAVRVGVHVBLLVPVGVHVBUAVGVHVBUB
      UCSUDSVPEAUEVHVMAEUKRVFVNACNVOVEVNACEVABUPDUFZTUGVNACUHUIREVABUNVSTAVACUQ
      UJULUM $.
      $( [9-Aug-2004] $)
  $}

  ${
    $d w x y z A $.  $d w x y z F $.
    $( The indexed union of a function's values is the union of its range.
       Compare Definition 5.4 of [Monk1] p. 50. $)
    fniunfv $p |- ( F Fn A -> U_ x e. A ( F ` x ) = U. ran F ) $=
      ( vy wfn crn cuni cv cfv wceq wrex cab ciun fnrnfv unieqd dfiun2 syl6reqr
      fvex ) CBEZCFZGDHAHZCIZJABKDLZGABUBMSTUCADBCNOADBUBUACRPQ $.
      $( [27-Sep-2004] $)

    $( The indexed union of a function's values is the union of its image under
       the index class.

       Note:  This theorem depends on the fact that our function value is the
       empty set outside of its domain.  If the antecedent is changed to
       ` F Fn A ` , the theorem can be proved without this dependency. $)
    funiunfv $p |- ( Fun F -> U_ x e. A ( F ` x ) = U. ( F " A ) ) $=
      ( vy vz vw cv cfv ciun wcel wceq wa cuni fvex wrex wex wi c0 wn syl5bb
      wfun copab crn cima fveq2 fvopab4 iuneq2i wfn fnopab2 fniunfv a1i syl5eqr
      eqid ax-mp cab rnopab2 unieqi eluniab wbr eqcom cdm n0i ndmfv eqeq1 con1d
      syl5ib impcom sylan vex funbrfvb biimpd sylan2 expr pm2.43d adantr impbid
      funbrfv rexbidv exbidv eluni elima anbi2i exbii bitr2i syl6bb eqrdv eqtrd
      pm5.32da syl5eq ) CUAZABAGZCHZIZDGZBJEGZWNCHZKZLDEUBZUCZMZCBUDZMZWJWMABWK
      WRHZIZWTABXCWLDEWKWPWLBWRWNWKCUEWRUMZWKCNUFUGXDWTKZWJWRBUHXFDEBWPWRWNCNXE
      UIABWRUJUNUKULWJWTWQDBOZEUOZMZXBWSXHDEBWPUPUQWJFXIXBFGZXIJXJWOJZXGLZEPZWJ
      XJXBJZXGEXJURWJXMXKWNWOCUSZDBOZLZEPZXNWJXLXQEWJXKXGXPWJXKLZWQXODBWQWPWOKZ
      XSXOWOWPUTXSXTXOXSXTXOWJXKXTXTXOQZXKXTLWJWNCVAJZYAXKWORKZSZXTYBWOXJVBXTYD
      YBXTYBYCYBSWPRKXTYCWNCVCWPWORVDVFVEVGVHWJYBLXTXOWNWOCEVIZVJVKVLVMVNWJXOXT
      QXKWNWOCYEVQVOVPTVRWHVSXNXKWOXAJZLZEPXREXJXAVTYGXQEYFXPXKDWOCBYEWAWBWCWDW
      ETWFWIWG $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x z A $.  $d y z F $.  $d x y $.
    funiunfvf.1 $e |- ( y e. F -> A. x y e. F ) $.
    $( The indexed union of a function's values is the union of its image under
       the index class.  This version of ~ funiunfv uses a bound-variable
       hypothesis in place of a distinct variable condition.  (Unnecessary
       distinct variable restrictions were removed by David Abernethy,
       15-Apr-2013.) $)
    funiunfvf $p |- ( Fun F -> U_ x e. A ( F ` x ) = U. ( F " A ) ) $=
      ( vz wfun cv ciun cima cuni wcel ax-17 hbfv fveq2 cbviun funiunfv syl5eqr
      cfv ) DGACAHZDSZIFCFHZDSZIDCJKFABCUCUAABUBDEBHZUBLAMNUDUALFMUBTDOPFCDQR
      $.
      $( [15-Apr-2013] $)  $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( Membership in the union of an image of a function. $)
    eluniima $p |- ( Fun F ->
                   ( B e. U. ( F " A ) <-> E. x e. A B e. ( F ` x ) ) ) $=
      ( cv cfv wcel wrex ciun wfun cima cuni eliun funiunfv eleq2d syl5rbbr ) C
      AEDFZGABHCABQIZGDJZCDBKLZGACBQMSRTCABDNOP $.
      $( [28-Sep-2006] $)

    $( Membership in the union of the range of a function. $)
    elunirn $p |- ( Fun F ->
                     ( A e. U. ran F <-> E. x e. dom F A e. ( F ` x ) ) ) $=
      ( crn cuni wcel cdm cima wfun cv cfv wrex imadmrn unieqi eluniima syl5bbr
      eleq2i ) BCDZEZFBCCGZHZEZFCIBAJCKFATLUBSBUARCMNQATBCOP $.
      $( [24-Sep-2006] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( Membership in the union of the range of a function, proved directly.
       Unlike ~ elunirn , it doesn't appeal to ~ ndmfv (via ~ funiunfv ). $)
    elunirnALT $p |- ( Fun F -> ( A e. U. ran F <->
                E. x e. dom F A e. ( F ` x ) ) ) $=
      ( vy crn cuni wcel cv wa wex wfun cfv cdm wrex eluni wceq wfn funfn eleq2
      wb fvelrnb sylbi anbi2d r19.42v syl6bbr biimparc reximi syl6bi fvelrn a1d
      exlimdv ancld fvex eleq1 anbi12d cla4ev syl6 rexlimdva impbid syl5bb ) BC
      EZFGBDHZGZVBVAGZIZDJZCKZBAHZCLZGZACMZNZDBVAOVGVFVLVGVEVLDVGVEVCVIVBPZIZAV
      KNZVLVGVEVCVMAVKNZIVOVGVDVPVCVGCVKQVDVPTCRAVKVBCUAUBUCVCVMAVKUDUEVNVJAVKV
      MVJVCVIVBBSUFUGUHUKVGVJVFAVKVGVHVKGIZVJVJVIVAGZIZVFVQVJVRVQVRVJVHCUIUJULV
      EVSDVIVHCUMVBVIPVCVJVDVRVBVIBSVBVIVAUNUOUPUQURUSUT $.
      $( [24-Sep-2006] $)
  $}

  ${
    $d x y z A $.  $d z ph $.
    abrexex2.1 $e |- A e. _V $.
    abrexex2.2 $e |- { y | ph } e. _V $.
    $( Existence of an existentially restricted class abstraction. ` ph ` is
       normally has free-variable parameters ` x ` and ` y ` .  See also
       ~ abrexex . $)
    abrexex2 $p |- { y | E. x e. A ph } e. _V $=
      ( vz wrex cab wcel cvv wsbc ax-17 hbs1 hbrex wceq sbequ12 rexbidv cbvab
      cv df-clab rexbii abbii eqtr4i ciun df-iun iunex eqeltrri eqeltri ) ABDHZ
      CIZGTZACIZJZBDHZGIZKUKACULLZBDHZGIUPUJURCGUJGMUQCBDBTDJCMACGNOCTULPAUQBDA
      CGQRSUOURGUNUQBDAGCUAUBUCUDBDUMUEUPKBGDUMUFBDUMEFUGUHUI $.
      $( [12-Sep-2004] $)

    $( Existence of a class abstraction with an existentially quantified
       expression.  Both ` x ` and ` y ` can be free in ` ph ` . $)
    abexssex $p |- { y | E. x ( x C_ A /\ ph ) } e. _V $=
      ( cpw wrex cab cv wss wa wex cvv wcel df-rex vex elpw anbi1i exbii bitri
      abbii pwex abrexex2 eqeltrri ) ABDGZHZCIBJZDKZALZBMZCINUGUKCUGUHUFOZALZBM
      UKABUFPUMUJBULUIAUHDBQRSTUAUBABCUFDEUCFUDUE $.
      $( [29-Jul-2006] $)
  $}

  ${
    $d x y A $.
    abexex.1 $e |- A e. _V $.
    abexex.2 $e |- ( ph -> x e. A ) $.
    abexex.3 $e |- { y | ph } e. _V $.
    $( A condition where a class builder continues to exist after its wff is
       existentially quantified. $)
    abexex $p |- { y | E. x ph } e. _V $=
      ( wrex cab wex cvv cv wcel wa df-rex pm4.71ri exbii bitr4i abbii abrexex2
      eqeltrri ) ABDHZCIABJZCIKUBUCCUBBLDMZANZBJUCABDOAUEBAUDFPQRSABCDEGTUA $.
      $( [4-Mar-2007] $)
  $}

  ${
    $d x y z A $.  $d z B $.  $d x y z F $.
    $( A one-to-one function in terms of function values.  Compare Theorem
       4.8(iv) of [Monk1] p. 43. $)
    dff13 $p |- ( F : A -1-1-> B <-> ( F : A --> B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( vz cv wbr wal wa cfv wceq wi wral wb wcel vex breldm eleq2d syl5ib fndm
      wf1 wmo dff12 wfn ffn cdm anim12d pm4.71rd eqcom fnbrfvb bi2anan9 anandis
      syl5bb pm5.32da bitr4d imbi1d impexp syl6bb albidv 19.21v wex 19.23v fvex
      wf eqvinc imbi1i bitr4i imbi2i bitri 2albidv breq1 mo4 albii alcom 3bitri
      r2al 3bitr4g syl pm5.32i ) CDEUBCDEVEZAGZFGZEHZAUCZFIZJWAWBEKZBGZEKZLZWBW
      HLZMZBCNACNZJAFCDEUDWAWFWMWAECUEZWFWMOCDEUFWNWDWHWCEHZJZWKMZFIZBIZAIZWBCP
      ZWHCPZJZWLMZBIAIWFWMWNWRXDABWNWRXCWCWGLZWCWILZJZWKMZMZFIZXDWNWQXIFWNWQXCX
      GJZWKMXIWNWPXKWKWNWPXCWPJXKWNWPXCWNWDXAWOXBWDWBEUGZPWNXAWBWCEAQRWNXLCWBCE
      UAZSTWOWHXLPWNXBWHWCEBQRWNXLCWHXMSTUHUIWNXCXGWPWNXAXBXGWPOWNXAJZXEWDWNXBJ
      ZXFWOXEWGWCLXNWDWCWGUJCWBWCEFQZUKUNXFWIWCLXOWOWCWIUJCWHWCEXPUKUNULUMUOUPU
      QXCXGWKURUSUTXJXCXHFIZMXDXCXHFVAXQWLXCXQXGFVBZWKMWLXGWKFVCWJXRWKFWGWIWBEV
      DVFVGVHVIVJUSVKWFWQBIZAIZFIXSFIZAIWTWEXTFWDWOABWBWHWCEVLVMVNXSFAVOYAWSAWQ
      FBVOVNVPWLABCCVQVRVSVTVJ $.
      $( [29-Oct-1996] $)
  $}

  ${
    $d x y w v A $.  $d w v B $.  $d z w v F $.  $d x y z $.
    f1fvf.1 $e |- ( z e. F -> A. x z e. F ) $.
    f1fvf.2 $e |- ( z e. F -> A. y z e. F ) $.
    $( A one-to-one function in terms of function values.  Compare Theorem
       4.8(iv) of [Monk1] p. 43. $)
    dff13f $p |- ( F : A -1-1-> B <-> ( F : A --> B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( vw vv cv cfv wceq wi wral wa wcel ax-17 hbfv hbim wf1 dff13 hbeq eqeq2d
      fveq2 eqeq2 imbi12d cbvral ralbii hbral eqeq1d eqeq1 ralbidv bitri anbi2i
      wf ) DEFUADEFUPZIKZFLZJKZFLZMZURUTMZNZJDOZIDOZPUQAKZFLZBKZFLZMZVGVIMZNZBD
      OZADOZPIJDEFUBVFVOUQVFUSVJMZURVIMZNZBDOZIDOVOVEVSIDVDVRJBDVBVCBBCCUSVABCU
      RFHCKZURQZBRSBCUTFHVTUTQBRSUCVCBRTVPVQJVPJRVQJRTUTVIMZVBVPVCVQWBVAVJUSUTV
      IFUEUDUTVIURUFUGUHUIVSVNIADVRABDVIDQARVPVQAACCUSVJACURFGWAARSACVIFGVTVIQA
      RSUCVQARTUJVNIRURVGMZVRVMBDWCVPVKVQVLWCUSVHVJURVGFUEUKURVGVIULUGUMUHUNUOU
      N $.
      $( [31-Jul-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x y F $.
    $( Equality of function values for a one-to-one function. $)
    f1fveq $p |- ( ( F : A -1-1-> B /\ ( C e. A /\ D e. A ) ) ->
                 ( ( F ` C ) = ( F ` D ) <-> C = D ) ) $=
      ( vx vy wf1 wcel wa cfv wceq wi cv fveq2 eqeq1d eqeq1 imbi12d imbi2d wral
      eqeq2d eqeq2 wf dff13 simprbi ra42 syl com12 vtocl2ga impcom impbid1 ) AB
      EHZCAIDAIJZJCEKZDEKZLZCDLZUMULUPUQMZULFNZEKZGNZEKZLZUSVALZMZMULUNVBLZCVAL
      ZMZMULURMFGCDAAUSCLZVEVHULVIVCVFVDVGVIUTUNVBUSCEOPUSCVAQRSVADLZVHURULVJVF
      UPVGUQVJVBUOUNVADEOUAVADCUBRSULUSAIVAAIJZVEULVEGATFATZVKVEMULABEUCVLFGABE
      UDUEVEFGAAUFUGUHUIUJCDEOUK $.
      $( [11-Feb-1997] $)
  $}

  ${
    $d F z $.  $d A z $.  $d Y z $.  $d X z $.  $d B z $.
    $( Membership in the image of a 1-1 map.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    f1elima $p |- ( ( F : A -1-1-> B /\ X e. A /\ Y C_ A )
                                -> ( ( F ` X ) e. ( F " Y ) <-> X e. Y ) ) $=
      ( vz wf1 wcel wss w3a cfv cima cv wceq wrex wb wfn wi wa anassrs fvelimab
      f1fn sylan 3adant2 ssel impac f1fveq ancom2s biimpd biimpcd sylan9 anasss
      eleq1 sylan2 rexlimdva 3impa fveq2 eqeq1d rcla4ev mpan2 impbid1 bitrd
      eqid ) ABCGZDAHZEAIZJZDCKZCELHZFMZCKZVHNZFEOZDEHZVDVFVIVMPZVEVDCAQVFVOABC
      UBFAEVHCUAUCUDVGVMVNVDVEVFVMVNRVDVESZVFSVLVNFEVPVFVJEHZVLVNRZVFVQSVPVJAHZ
      VQSVRVFVQVSEAVJUEUFVPVSVQVRVPVSSVLVJDNZVQVNVDVEVSVLVTRVDVEVSSSVLVTVDVSVEV
      LVTPABVJDCUGUHUITVTVQVNVJDEUMUJUKULUNTUOUPVNVHVHNZVMVHVCVLWAFDEVTVKVHVHVJ
      DCUQURUSUTVAVB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( A one-to-one onto function in terms of function values. $)
    dff1o6 $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ ran F = B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( wf1o wf1 wfo wa wf cv cfv wceq wi wral wfn crn w3a df-f1o dff13 anbi12i
      df-fo df-3an wss eqimss anim2i df-f sylibr pm4.71ri anbi1i 3bitrri 3bitri
      an32 ) CDEFCDEGZCDEHZICDEJZAKZELBKZELMUQURMNBCOACOZIZECPZEQZDMZIZIZVAVCUS
      RZCDESUNUTUOVDABCDETCDEUBUAVFVDUSIUPVDIZUSIVEVAVCUSUCVDVGUSVDUPVDVAVBDUDZ
      IUPVCVHVAVBDUEUFCDEUGUHUIUJUPVDUSUMUKUL $.
      $( [29-Mar-2008] $)
  $}

  $( The converse value of the value of a one-to-one onto function. $)
  f1ocnvfv1 $p |- ( ( F : A -1-1-onto-> B /\ C e. A ) ->
                 ( `' F ` ( F ` C ) ) = C ) $=
    ( wf1o wcel ccnv ccom cfv cid cres wceq f1ococnv1 fveq1d adantr wfun f1ocnv
    wa wf f1ofun syl f1of jca fvco3 3expa sylan fvresi adantl 3eqtr3d ) ABDEZCA
    FZRCDGZDHZIZCJAKZIZCDIULIZCUJUNUPLUKUJCUMUOABDMNOUJULPZABDSZRUKUNUQLZUJURUS
    UJBAULEURABDQBAULTUAABDUBUCURUSUKUTABCULDUDUEUFUKUPCLUJACUGUHUI $.
    $( [20-May-2004] $)

  $( The value of the converse value of a one-to-one onto function. $)
  f1ocnvfv2 $p |- ( ( F : A -1-1-onto-> B /\ C e. B ) ->
                 ( F ` ( `' F ` C ) ) = C ) $=
    ( wf1o wcel ccnv ccom cfv cid cres wceq f1ococnv2 fveq1d adantr wfun f1ofun
    wa wf f1ocnv f1of syl jca fvco3 3expa sylan fvresi adantl 3eqtr3d ) ABDEZCB
    FZRCDDGZHZIZCJBKZIZCULIDIZCUJUNUPLUKUJCUMUOABDMNOUJDPZBAULSZRUKUNUQLZUJURUS
    ABDQUJBAULEUSABDTBAULUAUBUCURUSUKUTBACDULUDUEUFUKUPCLUJBCUGUHUI $.
    $( [20-May-2004] $)

  $( Relationship between the value of a one-to-one onto function and the value
     of its converse.  (Contributed by Raph Levien, 10-Apr-2004.) $)
  f1ocnvfv $p |- ( ( F : A -1-1-onto-> B /\ C e. A ) ->
                  ( ( F ` C ) = D -> ( `' F ` D ) = C ) ) $=
    ( cfv wceq ccnv wf1o wcel wa fveq2 eqcoms f1ocnvfv1 eqeq2d syl5ib ) CEFZDGD
    EHZFZQRFZGZABEICAJKZSCGUADQDQRLMUBTCSABCENOP $.
    $( [10-Apr-2004] $)

  $( Relationship between the value of a one-to-one onto function and the value
     of its converse. $)
  f1ocnvfvb $p |- ( ( F : A -1-1-onto-> B /\ C e. A /\ D e. B ) ->
                  ( ( F ` C ) = D <-> ( `' F ` D ) = C ) ) $=
    ( wf1o wcel w3a cfv wceq ccnv wi f1ocnvfv 3adant3 wa fveq2 eqcoms f1ocnvfv2
    eqeq2d syl5ib 3adant2 impbid ) ABEFZCAGZDBGZHCEIZDJZDEKIZCJZUCUDUGUILUEABCD
    EMNUCUEUIUGLUDUIUFUHEIZJZUCUEOZUGUKCUHCUHEPQULUJDUFABDERSTUAUB $.
    $( [9-Aug-2006] $) $( [20-May-2004] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    $( There is one domain element for each value of a one-to-one onto
       function. $)
    f1ofveu $p |- ( ( F : A -1-1-onto-> B /\ C e. B ) ->
                  E! x e. A ( F ` x ) = C ) $=
      ( wf1o wcel wa cv cfv wceq wreu cop ccnv wf f1ocnv f1of syl wb wfn 3com23
      feu sylan w3a f1ocnvfvb dff1o4 simprbi vex fnopfvb 3adant3 bitrd reubidva
      syl3an1 3expa mpbird ) BCEFZDCGZHZAIZEJDKZABLDUSMENZGZABLZUPCBVAOZUQVCUPC
      BVAFVDBCEPCBVAQRACBDVAUBUCURUTVBABUPUQUSBGZUTVBSUPUQVEUDUTDVAJUSKZVBUPVEU
      QUTVFSBCUSDEUEUAUPVACTZUQVEVFVBSZUPEBTVGBCEUFUGVGUQVHVECDUSVAAUHUIUJUMUKU
      NULUO $.
      $( [26-May-2006] $)

    $( Value of the converse of a one-to-one onto function. $)
    f1ocnvfv3 $p |- ( ( F : A -1-1-onto-> B /\ C e. B ) ->
                  ( `' F ` C ) = U. { x e. A | ( F ` x ) = C } ) $=
      ( wf1o wcel wa ccnv cfv wceq cv crab cuni f1ocnvfv2 wreu wb f1ocnv f1of
      wf ffvelrn sylan f1ofveu fveq2 eqeq1d reuuni2 eqcom syl6bb syl2anc mpbid
      syl ) BCEFZDCGZHZDEIZJZEJZDKZUPALZEJZDKZABMNZKZBCDEOUNUPBGZVAABPZURVCQULC
      BUOTZUMVDULCBUOFVFBCERCBUOSUKCBDUOUAUBABCDEUCVDVEHURVBUPKVCVAURABUPUSUPKU
      TUQDUSUPEUDUEUFVBUPUGUHUIUJ $.
      $( [26-May-2006] $)
  $}

  $( The value of the converse of a one-to-one onto function belongs to its
     domain. $)
  f1ocnvdm $p |- ( ( F : A -1-1-onto-> B /\ C e. B ) ->
                    ( `' F ` C ) e. A ) $=
    ( wf1o ccnv wf wcel cfv f1ocnv f1of syl ffvelrn sylan ) ABDEZBADFZGZCBHCPIA
    HOBAPEQABDJBAPKLBACPMN $.
    $( [26-May-2006] $)

  ${
    $d x A $.  $d y B $.  $d x y F $.  $d y ph $.  $d x ps $.
    cbvfo.1 $e |- ( ( F ` x ) = y -> ( ph <-> ps ) ) $.
    $( Change bound variable between domain and range of function. $)
    cbvfo $p |- ( F : A -onto-> B -> ( A. x e. A ph <-> A. y e. B ps ) ) $=
      ( wral wb cv wcel wi wal wceq wa wex vex a1i jcad wfo cdm wfun fofun elrn
      crn cfv wbr breldm funbrfv eximdv syl5bi hba1 ax-17 biimpcd imim2i exlimd
      imp3a a4s syl9 alrimdv eldm brelrn biimprcd impbid df-ral 3bitr4g syl fof
      wf fdm raleq 3syl forn raleqdv 3bitr3d ) EFGUAZACGUBZIZBDGUFZIZACEIZBDFIV
      QGUCZVSWAJEFGUDWCCKZVRLZAMZCNZDKZVTLZBMZDNZVSWAWCWGWKWCWGWJDWCWIWEWDGUGWH
      OZPZCQZWGBWIWDWHGUHZCQWCWNCWHGDRZUEWCWOWMCWCWOWEWLWOWEMWCWDWHGCRZUISWDWHG
      WPUJZTUKULWGWMBCWFCUMBCUNWFWMBMCWFWEWLBAWLBMWEWLABHUOUPURUSUQUTVAWCWKWFCW
      CWEWIWLPZDQZWKAWEWODQWCWTDWDGWQVBWCWOWSDWCWOWIWLWOWIMWCWDWHGWQWPVCSWRTUKU
      LWKWSADWJDUMADUNWJWSAMDWJWIWLABWLAMWIWLABHVDUPURUSUQUTVAVEACVRVFBDVTVFVGV
      HVQEFGVJVREOVSWBJEFGVIEFGVKACVREVLVMVQBDVTFEFGVNVOVP $.
      $( [23-Feb-1997] $)

    $( Change bound variable between domain and range of function. $)
    cbvexfo $p |- ( F : A -onto-> B -> ( E. x e. A ph <-> E. y e. B ps ) ) $=
      ( wfo wn wral wrex cv cfv wceq notbid cbvfo dfrex2 3bitr4g ) EFGIZAJZCEKZ
      JBJZDFKZJACELBDFLTUBUDUAUCCDEFGCMGNDMOABHPQPACERBDFRS $.
      $( [23-Feb-1997] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y H $.  $d x y G $.
    $d x y R $.  $d x y S $.  $d x y T $.
    $( Equality theorem for isomorphisms. $)
    isoeq1 $p |- ( H = G ->
          ( H Isom R , S ( A , B ) <-> G Isom R , S ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq1 fveq1 df-iso 2ralbidv
      breq12d bibi2d anbi12d 3bitr4g ) FEIZABFJZGKZHKZCLZUHFMZUIFMZDLZNZHAOGAOZ
      PABEJZUJUHEMZUIEMZDLZNZHAOGAOZPABCDFQABCDEQUFUGUPUOVAABFERUFUNUTGHAAUFUMU
      SUJUFUKUQULURDUHFESUIFESUBUCUAUDGHABCDFTGHABCDETUE $.
      $( [17-May-2004] $)

    $( Equality theorem for isomorphisms. $)
    isoeq2 $p |- ( R = T ->
          ( H Isom R , S ( A , B ) <-> H Isom T , S ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso breq bibi1d df-iso 2ralbidv
      anbi2d 3bitr4g ) CEIZABFJZGKZHKZCLZUFFMUGFMDLZNZHAOGAOZPUEUFUGELZUINZHAOG
      AOZPABCDFQABEDFQUDUKUNUEUDUJUMGHAAUDUHULUIUFUGCERSUAUBGHABCDFTGHABEDFTUC
      $.
      $( [17-May-2004] $)

    $( Equality theorem for isomorphisms. $)
    isoeq3 $p |- ( S = T ->
          ( H Isom R , S ( A , B ) <-> H Isom R , T ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso breq bibi2d df-iso 2ralbidv
      anbi2d 3bitr4g ) DEIZABFJZGKZHKZCLZUFFMZUGFMZDLZNZHAOGAOZPUEUHUIUJELZNZHA
      OGAOZPABCDFQABCEFQUDUMUPUEUDULUOGHAAUDUKUNUHUIUJDERSUAUBGHABCDFTGHABCEFTU
      C $.
      $( [17-May-2004] $)

    $( Equality theorem for isomorphisms. $)
    isoeq4 $p |- ( A = C ->
          ( H Isom R , S ( A , B ) <-> H Isom R , S ( C , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq2 raleq df-iso anbi12d
      raleqbi1dv 3bitr4g ) ACIZABFJZGKZHKZDLUFFMUGFMELNZHAOZGAOZPCBFJZUHHCOZGCO
      ZPABDEFQCBDEFQUDUEUKUJUMACBFRUIULGACUHHACSUBUAGHABDEFTGHCBDEFTUC $.
      $( [17-May-2004] $)

    $( Equality theorem for isomorphisms. $)
    isoeq5 $p |- ( B = C ->
          ( H Isom R , S ( A , B ) <-> H Isom R , S ( A , C ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq3 anbi1d df-iso 3bitr4g
      ) BCIZABFJZGKZHKZDLUDFMUEFMELNHAOGAOZPACFJZUFPABDEFQACDEFQUBUCUGUFBCAFRSG
      HABDEFTGHACDEFTUA $.
      $( [17-May-2004] $)
  $}


  ${
    $d y z w H $.  $d y z w R $.  $d y z w S $.  $d y z w A $.  $d y z w B $.
    $d x y z w $.
    hbiso.1 $e |- ( y e. H -> A. x y e. H ) $.
    hbiso.2 $e |- ( y e. R -> A. x y e. R ) $.
    hbiso.3 $e |- ( y e. S -> A. x y e. S ) $.
    hbiso.4 $e |- ( y e. A -> A. x y e. A ) $.
    hbiso.5 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for an isomorphism.  (The proof was
       shortened by Andrew Salmon, 22-Oct-2011.) $)
    hbiso $p |- ( H Isom R , S ( A , B ) -> A. x H Isom R , S ( A , B ) ) $=
      ( vz vw cv wbr cfv wral hblem wcel wiso wf1o wb wa df-iso hbf1o hbbr hbfv
      ax-17 hbbi hbral hban hbxfrbi ) CDEFGUACDGUBZMOZNOZEPZUOGQZUPGQZFPZUCZNCR
      ZMCRZUDAMNCDEFGUEUNVCAABCDGHKLUFVBAMCABMCKSVAANCABNCKSUQUTAABUOUPEBOZUOTA
      UIZIVDUPTAUIZUGABURUSFABUOGHVEUHJABUPGHVFUHUGUJUKUKULUM $.
      $( [22-Oct-2011] $) $( [17-May-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $( An isomorphism is a one-to-one onto function. $)
    isof1o $p |- ( H Isom R , S ( A , B ) -> H : A -1-1-onto-> B ) $=
      ( vx vy wiso wf1o cv wbr cfv wb wral df-iso simplbi ) ABCDEHABEIFJZGJZCKQ
      ELRELDKMGANFANFGABCDEOP $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $d x y C $.  $d x y D $.
    $( An isomorphism connects binary relations via its function values. $)
    isorel $p |- ( ( H Isom R , S ( A , B ) /\ ( C e. A /\ D e. A ) ) ->
                 ( C R D <-> ( H ` C ) S ( H ` D ) ) ) $=
      ( vx vy wiso wcel wa wbr cfv wb cv wral wceq fveq2 bibi12d df-iso simprbi
      wf1o breq1 breq1d breq2 breq2d rcla42v syl5com imp ) ABEFGJZCAKDAKLZCDEMZ
      CGNZDGNZFMZOZUKHPZIPZEMZURGNZUSGNZFMZOZIAQHAQZULUQUKABGUCVEHIABEFGUAUBVDU
      QCUSEMZUNVBFMZOHICDAAURCRZUTVFVCVGURCUSEUDVHVAUNVBFURCGSUETUSDRZVFUMVGUPU
      SDCEUFVIVBUOUNFUSDGSUGTUHUIUJ $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    $( Identity law for isomorphism.  Proposition 6.30(1) of [TakeutiZaring]
       p. 33. $)
    isoid $p |- ( _I |` A ) Isom R , R ( A , A ) $=
      ( vx vy cid cres wiso wf1o wbr cfv wral df-iso f1oi wcel fvresi breqan12d
      cv wb wa bicomd rgen2a mpbir2an ) AABBEAFZGAAUCHCQZDQZBIZUDUCJZUEUCJZBIZR
      ZDAKCAKCDAABBUCLAMUJCDAUDANZUEANZSUIUFUKULUGUDUHUEBAUDOAUEOPTUAUB $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w R $.  $d x y z w S $.
    $d x y z w H $.
    $( Converse law for isomorphism.  Proposition 6.30(2) of [TakeutiZaring]
       p. 33. $)
    isocnv $p |- ( H Isom R , S ( A , B ) -> `' H Isom S , R ( B , A ) ) $=
      ( vx vy vz vw wf1o cv wbr cfv wb wral wa wiso wcel wceq f1ocnvfv2 adantrr
      ccnv f1ocnv adantr adantrl breq12d adantlr wf syl ffvelrn anim12i anandis
      f1of breq1 fveq2 breq1d bibi12d bicom syl6bb breq2d breq2 imp sylan an32s
      rcla42v sylanl1 bitr3d exp32 ralrimdv ralrimiv jca df-iso 3imtr4i ) ABEJZ
      FKZGKZCLZVOEMZVPEMZDLZNZGAOFAOZPZBAEUBZJZHKZIKZDLZWFWDMZWGWDMZCLZNZIBOZHB
      OZPABCDEQBADCWDQWCWEWNVNWEWBABEUCZUDWCWMHBWCWFBRZWLIBWCWPWGBRZWLWCWPWQPZP
      WIEMZWJEMZDLZWHWKVNWRXAWHNWBVNWRPWSWFWTWGDVNWPWSWFSWQABWFETUAVNWQWTWGSWPA
      BWGETUEUFUGVNBAWDUHZWBWRXAWKNZVNWEXBWOBAWDUMUIXBWRWBXCXBWRPWIARZWJARZPZWB
      XCXBWPWQXFXBWPPXDXBWQPXEBAWFWDUJBAWGWDUJUKULXFWBXCWAXCWSVSDLZWIVPCLZNZFGW
      IWJAAVOWISZWAXHXGNXIXJVQXHVTXGVOWIVPCUNXJVRWSVSDVOWIEUOUPUQXHXGURUSVPWJSZ
      XGXAXHWKXKVSWTWSDVPWJEUOUTVPWJWICVAUQVEVBVCVDVFVGVHVIVJVKFGABCDEVLHIBADCW
      DVLVM $.
      $( [27-Apr-2004] $)
    $( Converse law for isomorphism.  (Contributed by Mario Carneiro,
       30-Jan-2014.) $)
    isocnv2 $p |- ( H Isom R , S ( A , B ) <->
                    H Isom `' R , `' S ( A , B ) ) $=
      ( vy vx wf1o cv wbr cfv wb wral wa ccnv wiso vex brcnv fvex df-iso ralcom
      bibi12i 2ralbii bitr4i anbi2i 3bitr4i ) ABEHZFIZGIZCJZUHEKZUIEKZDJZLZGAMF
      AMZNUGUIUHCOZJZULUKDOZJZLZFAMGAMZNABCDEPABUPUREPUOVAUGUOUNFAMGAMVAUNFGAAU
      AUTUNGFAAUQUJUSUMUIUHCGQFQRULUKDUIESUHESRUBUCUDUEFGABCDETGFABUPURETUF $.
      $( [30-Jan-2014] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d H x y $.  $d R x y $.  $d S x y $.
    $( An isomorphism from one well-order to another can be restricted on
       either well-order.  (Contributed by Mario Carneiro, 15-Jan-2013.) $)
    isores2 $p |- ( H Isom R , S ( A , B ) <->
                      H Isom R , ( S i^i ( B X. B ) ) ( A , B ) ) $=
      ( vx vy wf1o cv wbr cfv wb wral wa cxp wiso wcel ffvelrn ralbidva df-iso
      cin wf f1of adantrr adantrl brinxp syl2anc anassrs bibi2d pm5.32i 3bitr4i
      sylan ) ABEHZFIZGIZCJZUNEKZUOEKZDJZLZGAMZFAMZNUMUPUQURDBBOUAZJZLZGAMZFAMZ
      NABCDEPABCVCEPUMVBVGUMVAVFFAUMUNAQZNZUTVEGAVIUOAQZNUSVDUPUMVHVJUSVDLZUMAB
      EUBZVHVJNZVKABEUCVLVMNUQBQZURBQZVKVLVHVNVJABUNERUDVLVJVOVHABUOERUEUQURBBD
      UFUGULUHUISSUJFGABCDETFGABCVCETUK $.
      $( [15-Jan-2013] $)
  $}

  ${
    $( An isomorphism from one well-order to another can be restricted on
       either well-order.  (Contributed by Mario Carneiro, 15-Jan-2013.) $)
    isores1 $p |- ( H Isom R , S ( A , B ) <->
                      H Isom ( R i^i ( A X. A ) ) , S ( A , B ) ) $=
      ( wiso cxp cin ccnv isocnv isores2 sylib wf1o wb isof1o isoeq1 sylbi 3syl
      syl mpbid wrel f1orel wceq dfrel2 sylibr impbii ) ABCDEFZABCAAGHZDEFZUGAB
      UHDEIZIZFZUIUGBADUHUJFZULUGBADCUJFZUMABCDEJBADCUJKZLBADUHUJJSUGABEMZEUAZU
      LUINZABCDEOABEUBZUQUKEUCZUREUDZABUHDEUKPQRTUIABCDUKFZUGUIUNVBUIUMUNABUHDE
      JUOUEBADCUJJSUIUPUQVBUGNZABUHDEOUSUQUTVCVAABCDEUKPQRTUF $.
      $( [15-Jan-2013] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u R $.  $d x y z w v u S $.  $d x y z w v u T $.
    $d x y z w v u G $.  $d x y z w v u H $.
    $( Composition (transitive) law for isomorphism.  Proposition 6.30(3) of
       [TakeutiZaring] p. 33. $)
    isotr $p |- ( ( H Isom R , S ( A , B ) /\ G Isom S , T ( B , C ) ) ->
               ( G o. H ) Isom R , T ( A , C ) ) $=
      ( vz vw vv vu vx cv wbr cfv wb wral wa wceq wf1o ccom wiso anim12i ancoms
      vy simpl f1oco syl wcel wi wf f1of ffvelrn ex anim12d adantr breq1 breq1d
      fveq2 bibi12d breq2 breq2d rcla42v com12 adantl sylan9 imp impcom adantll
      adantlr f1ofun fvco3 3expa breqan12d anandis sylan 3bitr4d exp32 ralrimdv
      wfun ralrimiv jca df-iso anbi12i 3imtr4i ) ABHUAZINZJNZDOZWHHPZWIHPZEOZQZ
      JARIARZSZBCGUAZKNZLNZEOZWRGPZWSGPZFOZQZLBRKBRZSZSZACGHUBZUAZMNZUFNZDOZXJX
      HPZXKXHPZFOZQZUFARZMARZSABDEHUCZBCEFGUCZSACDFXHUCXGXIXRXGWQWGSZXIXFWPYAXF
      WQWPWGWQXEUGWGWOUGUDUEZABCGHUHUIXGXQMAXGXJAUJZXPUFAXGYCXKAUJZXPXGYCYDSZSX
      JHPZXKHPZEOZYFGPZYGGPZFOZXLXOXGYEYHYKQZWPYEYFBUJZYGBUJZSZXFYLWGYEYOUKZWOW
      GABHULZYPABHUMZYQYCYMYDYNYQYCYMABXJHUNUOYQYDYNABXKHUNUOUPUIUQXEYOYLUKWQYO
      XEYLXDYLYFWSEOZYIXBFOZQKLYFYGBBWRYFTZWTYSXCYTWRYFWSEURUUAXAYIXBFWRYFGUTUS
      VAWSYGTZYSYHYTYKWSYGYFEVBUUBXBYJYIFWSYGGUTVCVAVDVEVFVGVHWPYEXLYHQZXFWOYEU
      UCWGYEWOUUCWNUUCXJWIDOZYFWLEOZQIJXJXKAAWHXJTZWJUUDWMUUEWHXJWIDURUUFWKYFWL
      EWHXJHUTUSVAWIXKTZUUDXLUUEYHWIXKXJDVBUUGWLYGYFEWIXKHUTVCVAVDVIVJVKXGYAYEX
      OYKQZYBYAGWAZYQSZYEUUHWQUUIWGYQBCGVLYRUDUUJYCYDUUHUUJYCSUUJYDSXMYIXNYJFUU
      IYQYCXMYITABXJGHVMVNUUIYQYDXNYJTABXKGHVMVNVOVPVQVQVRVSVTWBWCXSWPXTXFIJABD
      EHWDKLBCEFGWDWEMUFACDFXHWDWF $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w R $.
    $d x y z w S $.  $d x y z w T $.  $d x y z w G $.  $d x y z w H $.
    $( Composition (transitive) law for isomorphism.  Proposition 6.30(3) of
       [TakeutiZaring] p. 33.  This proof is shorter than ~ isotr in set.mm and
       uses fewer dummy variables, but it takes 240K vs. 207K for the web
       page. $)
    isotrALT $p |- ( ( H Isom R , S ( A , B ) /\ G Isom S , T ( B , C ) ) ->
               ( G o. H ) Isom R , T ( A , C ) ) $=
      ( vx vy vz vw cv wbr cfv wb wral wa ax-17 hban wf1o ccom wiso simpl f1oco
      anim12i ancoms hbra1 wcel hbral wi wf f1of ffvelrn ex anim12d adantr wceq
      syl breq1 fveq2 breq1d bibi12d breq2 breq2d rcla42v com12 adantl imp ra42
      sylan9 adantll adantlr f1ofun fvco3 3expa breqan12d anandis sylan 3bitr4d
      wfun exp32 ralrimd ralrimi jca df-iso anbi12i 3imtr4i ) ABHUAZIMZJMZDNZWJ
      HOZWKHOZENZPZJAQZIAQZRZBCGUAZKMZLMZENZXAGOZXBGOZFNZPZLBQKBQZRZRZACGHUBZUA
      ZWLWJXKOZWKXKOZFNZPZJAQZIAQZRABDEHUCZBCEFGUCZRACDFXKUCXJXLXRXJWTWIRZXLXIW
      SYAXIWTWSWIWTXHUDWIWRUDUFUGZABCGHUEUSXJXQIAWSXIIWIWRIWIISWQIAUHTXIISTXJWJ
      AUIZXPJAWSXIJWIWRJWIJSWQJIAYCJSZWPJAUHUJTXIJSTYDXJYCWKAUIZXPXJYCYERZRWOWM
      GOZWNGOZFNZWLXOXJYFWOYIPZWSYFWMBUIZWNBUIZRZXIYJWIYFYMUKZWRWIABHULZYNABHUM
      ZYOYCYKYEYLYOYCYKABWJHUNUOYOYEYLABWKHUNUOUPUSUQXHYMYJUKWTYMXHYJXGYJWMXBEN
      ZYGXEFNZPKLWMWNBBXAWMURZXCYQXFYRXAWMXBEUTYSXDYGXEFXAWMGVAVBVCXBWNURZYQWOY
      RYIXBWNWMEVDYTXEYHYGFXBWNGVAVEVCVFVGVHVKVIWSYFWPXIWRYFWPWIWRYFWPWPIJAAVJV
      IVLVMXJYAYFXOYIPZYBYAGWAZYORZYFUUAWTUUBWIYOBCGVNYPUFUUCYCYEUUAUUCYCRUUCYE
      RXMYGXNYHFUUBYOYCXMYGURABWJGHVOVPUUBYOYEXNYHURABWKGHVOVPVQVRVSVSVTWBWCWDW
      EXSWSXTXIIJABDEHWFKLBCEFGWFWGIJACDFXKWFWH $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $d x y C $.  $d x y D $.
    $( Isomorphisms preserve minimal elements.  Note that ` ( ``' R " { D } ) `
       is Takeuti and Zaring's idiom for the initial segment
       ` { x | x R D } ` .  Proposition 6.31(1) of [TakeutiZaring] p. 33. $)
    isomin $p |- ( ( H Isom R , S ( A , B ) /\ ( C C_ A /\ D e. A ) ) ->
               ( ( C i^i ( `' R " { D } ) ) = (/) <->
               ( ( H " C ) i^i ( `' S " { ( H ` D ) } ) ) = (/) ) ) $=
      ( vy vx wcel wa cima wceq wex wrex wbr wb wi syl9r cvv wss csn cin c0 cfv
      wiso ccnv wn cv neq0 ssel wf1o wfn isof1o f1ofn vex fnbrfvb ex 3syl imp31
      rexbidva elima syl6rbbr fvex eliniseg a1i anbi12d r19.41v 3bitr4g adantrr
      ax-mp elin breq1 biimpar isorel bitrd syl5ibr exp32 com34 imp32 reximdvai
      ad2antll sylbid exbii df-rex 3bitr4i syl6ibr exlimdv con4d com12 funfvima
      syl5bi syl funfni com13 syld sylan adantrd biimpd imp3a jcad 3imtr4g syl6
      imp n0i impcon4bid ) ABEFGUFZCAUAZDAJZKZKZCEUGDUBLZUCZUDMZGCLZFUGDGUEZUBL
      ZUCZUDMZXKXSXNXSUHZHUIZXRJZHNXKXNUHZHXRUJXKYBYCHXKYBIUIZXLJZICOZYCXKYBYDG
      UEZYAMZYAXPFPZKZICOZYFXGXHYBYKQXIXGXHKZYAXOJZYAXQJZKYHICOZYIKYBYKYLYMYOYN
      YIYLYOYDYAGPZICOYMYLYHYPICXGXHYDCJZYHYPQZXHYQYDAJZXGYRCAYDUKZXGABGULZGAUM
      ZYSYRRABEFGUNZABGUOZUUBYSYRAYDYAGHUPZUQURUSSUTVAIYAGCUUEVBVCYNYIQZYLXPTJZ
      UUFDGVDZFXPYATUUEVEVKVFVGYAXOXQVLYHYIICVHVIVJXKYJYEICXGXHXIYQYJYERZRXGXHY
      QXIUUIXHYQYSXGXIUUIRYTXGYSXIUUIYJYEXGYSXIKKZYGXPFPZYHUUKYIYGYAXPFVMVNUUJY
      EYDDEPZUUKXIYEUULQXGYSEDYDAIUPVEWBZABYDDEFGVOZVPVQVRSVSVTWAWCYDXMJZINZYQY
      EKZINYCYFUUOUUQIYDCXLVLZWDIXMUJZYEICWEWFWGWHWLWIYCUUPXKXTUUSXKUUOXTIXKUUO
      YGXRJZXTXKUUQYGXOJZYGXQJZKUUOUUTXKUUQUVAUVBXKYQUVAYEXGUUBXJYQUVARZXGUUAUU
      BUUCUUDWMUUBXHUVCXIUUBXHUVCYQXHUUBUVAYQXHYSUUBUVARXHYQYSYTWJUUBYSYQUVAUUB
      YSUVCUVCAYDGCYDGWKWNURWOWPWOXDVJWQWRXKYQYEUVBXGXHXIYQYEUVBRZRXGXHYQXIUVDX
      HYQYSXGXIUVDRYTXGYSXIUVDUUJYEUULUVBUUMUUJUULUUKUVBUUJUULUUKUUNWSUUGUVBUUK
      QUUHFXPYGTYDGVDVEVKWGWCVRSVSVTWTXAUURYGXOXQVLXBXRYGXEXCWHWLXF $.
      $( [19-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $d x y D $.
    $( Isomorphisms preserve initial segments.  Proposition 6.31(2) of
       [TakeutiZaring] p. 33. $)
    isoini $p |- ( ( H Isom R , S ( A , B ) /\ D e. A ) ->
               ( H " ( A i^i ( `' R " { D } ) ) ) =
               ( B i^i ( `' S " { ( H ` D ) } ) ) ) $=
      ( vx vy wcel wa ccnv cfv csn cima cin cv wbr wrex wb syl6bb wiso cab elin
      wceq crn wf1o wfo isof1o f1ofo forn eleq2d 3syl f1ofn fvelrnb bitr3d fvex
      wfn cvv vex eliniseg ax-mp a1i anbi12d adantr anbi2d syl5bb anbi1d adantl
      anass wi isorel syl fnbrfvb sylan adantrr ancom breq1 pm5.32i bitri exp32
      bicomd com23 imp pm5.32d rexbidv2 r19.41v bitr4d abbi2dv dfima2 syl6reqr
      bitrd ) ABDEFUAZCAIZJZBEKCFLZMNZOZGPZHPZFQZGADKCMNZOZRZHUBFXBNWNXCHWQWSWQ
      IWSBIZWSWPIZJZWNXCWSBWPUCWNXFWRFLZWSUDZGARZWSWOEQZJZXCWLXFXKSWMWLXDXIXEXJ
      WLWSFUEZIZXDXIWLABFUFZABFUGZXMXDSABDEFUHZABFUIXOXLBWSABFUJUKULWLXNFAUQZXM
      XISXPABFUMZGAWSFUNULUOXEXJSZWLWOURIXSCFUPEWOWSURHUSZUTVAVBVCVDWNXCXHXJJZG
      ARXKWNWTYAGXBAWNWRXBIZWTJZWRAIZWRCDQZWTJZJZYDYAJWMYCYGSWLWMYCYDYEJZWTJYGW
      MYBYHWTYBYDWRXAIZJWMYHWRAXAUCWMYIYEYDDCWRAGUSUTVEVFVGYDYEWTVITVHWNYDYFYAW
      LWMYDYFYASZVJWLYDWMYJWLYDWMYJWLYDWMJJZYFXGWOEQZXHJZYAYKYEYLWTXHABWRCDEFVK
      WLYDWTXHSZWMWLXQYDYNWLXNXQXPXRVLXQYDJXHWTAWRWSFXTVMWAVNVOVCYMXHYLJYAYLXHV
      PXHYLXJXGWSWOEVQVRVSTVTWBWCWDWKWEXHXJGAWFTWGVFWHGHFXBWIWJ $.
      $( [20-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x y H $.
    $d x y R $.  $d x y S $.
    isoini2.1 $e |- C = ( A i^i ( `' R " { X } ) ) $.
    isoini2.2 $e |- D = ( B i^i ( `' S " { ( H ` X ) } ) ) $.
    $( Isomorphisms are isomorphisms on their initial segments.  (Contributed
       by Mario Carneiro, 29-Mar-2014.) $)
    isoini2 $p |- ( ( H Isom R , S ( A , B ) /\ X e. A ) ->
                    ( H |` C ) Isom R , S ( C , D ) ) $=
      ( vx vy wiso wcel wf1o wbr cfv wb wral cima wa cres cv wf1 wss isof1o syl
      f1of1 adantr ccnv csn inss1 eqsstri f1ores sylancl isoini imaeq2i 3eqtr4g
      cin wceq f1oeq3 mpbid df-iso simprbi ssralv ralimdv mpsyl fvres breqan12d
      bibi2d ralbidva ralbiia sylibr sylanbrc ) ABEFGMZHANZUAZCDGCUBZOZKUCZLUCZ
      EPZVTVRQZWAVRQZFPZRZLCSZKCSZCDEFVRMVQCGCTZVROZVSVQABGUDZCAUEZWJVOWKVPVOAB
      GOZWKABEFGUFABGUHUGUICAEUJHUKTZUSZAIAWNULUMZABCGUNUOVQWIDUTWJVSRVQGWOTBFU
      JHGQUKTUSWIDABHEFGUPCWOGIUQJURWIDCVRVAUGVBVQWBVTGQZWAGQZFPZRZLCSZKCSZWHWL
      VQXAKASZXBWPWLVQWTLASZKASZXCWPVOXEVPVOWMXEKLABEFGVCVDUIWLXDXAKAWTLCAVEVFV
      GXAKCAVEVGWGXAKCVTCNZWFWTLCXFWACNZUAWEWSWBXFXGWCWQWDWRFVTCGVHWACGVHVIVJVK
      VLVMKLCDEFVRVCVN $.
      $( [29-Mar-2014] $)
  $}

  ${
    $d x y w A $.  $d x y z w B $.  $d x y w R $.  $d x y z w S $.
    $d x y z w H $.
    $( Lemma for ~ isofr . $)
    isofrlem $p |- ( H Isom R , S ( A , B ) -> ( S Fr B -> R Fr A ) ) $=
      ( vx vy vw vz cv wss c0 wa cima cin wceq wrex wi wcel syl wfr wne csn wal
      wiso ccnv wf1o isof1o wfn f1ofn wex n0 ssel cfv funfvima funfni ne0i syl6
      sylan9r pm2.43d exlimdv syl5bi expimpd wfo f1ofo crn imassrn sseq2d mpbii
      forn jctild dffr3 wfun f1ofun cvv vex funimaex sseq1 neeq1 anbi12d eqeq1d
      ex ineq1 rexeqbi1dv imbi12d cla4gv 3syl syl5d adantr simpl fvelima syl2an
      simpr wb imdistani isomin sylan2 sneq imaeq2d ineq2d sylan9bb syl5ibr imp
      exp42 com3l com4t reximdvai mpd rexlimdv adantrd a2d syld alrimdv syl6ibr
      exp32 ) ABCDEUEZBDUAZFJZAKZXRLUBZMZXRCUFGJZUCNOLPZGXRQZRZFUDACUAXPXQYEFXP
      XQYAEXRNZDUFZHJZUCZNZOZLPZHYFQZRYEXPYAYFBKZYFLUBZMZXQYMXPABEUGZYAYPRABCDE
      UHZYQYAYOYNYQEAUIZYAYORABEUJYSXSXTYOXTYBXRSZGUKYSXSMZYOGXRULUUAYTYOGUUAYT
      YOXSYTYBASZYSYTYORZXRAYBUMZYSUUBUUCYSUUBMYTYBEUNZYFSZYOYTUUFRAYBEXRYBEUOU
      PYFUUEUQURWBUSUTVAVBVCTYQABEVDZYNABEVEUUGYFEVFZKYNEXRVGUUGUUHBYFABEVJVHVI
      TVKTXQIJZBKZUUILUBZMZUUIYJOZLPZHUUIQZRZIUDZXPYPYMRZIHBDVLXPYQEVMZUUQUURRZ
      YRABEVNZUUSYFVOSUUTEXRFVPVQUUPUURIYFVOUUIYFPZUULYPUUOYMUVBUUJYNUUKYOUUIYF
      BVRUUIYFLVSVTUUNYLHUUIYFUVBUUMYKLUUIYFYJWCWAWDWEWFTWGVBWHXPYAYMYDXPXSYMYD
      RZXTXPXSUVCXPXSMZYLYDHYFUVDYHYFSZYLYDUVDUVEYLMZMZUUEYHPZGXRQZYDUVDUUSUVEU
      VIUVFUVDYQUUSXPYQXSYRWIUVATUVEYLWJGYHXREWKWLUVGUVHYCGXRUVDUVFYTUVHYCRRYTU
      VHUVDUVFYCUVDYTUVHUVFYCRZXPXSYTUVHUVJRRXPXSYTUVHUVJUVFYCXPXSYTMZMZUVHMYLU
      VEYLWMUVLYCYFYGUUEUCZNZOZLPZUVHYLUVKXPXSUUBMYCUVPWNXSYTUUBUUDWOABXRYBCDEW
      PWQUVHUVOYKLUVHUVNYJYFUVHUVMYIYGUUEYHWRWSWTWAXAXBXDXCXEXFXCXGXHXOXIWBXJXK
      XLXMFGACVLXN $.
      $( [29-Apr-2004] $)
  $}

  $( An isomorphism preserves foundedness.  Proposition 6.32(1) of
     [TakeutiZaring] p. 33. $)
  isofr $p |- ( H Isom R , S ( A , B ) -> ( R Fr A <-> S Fr B ) ) $=
    ( wiso wfr ccnv wi isocnv isofrlem syl impbid ) ABCDEFZACGZBDGZNBADCEHZFOPI
    ABCDEJBADCQKLABCDEKM $.
    $( [30-Apr-2004] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y R $.  $d x y z w S $.
    $d x y z w H $.
    $( An isomorphism preserves well ordering.  Proposition 6.32(3) of
       [TakeutiZaring] p. 33. $)
    isowe $p |- ( H Isom R , S ( A , B ) -> ( R We A <-> S We B ) ) $=
      ( vx vy vz vw wfr cv wbr wceq w3o wral wa wwe cfv wb 3orbi123d wiso isofr
      wcel isorel wf1 wf1o isof1o f1of1 syl f1fveq bicomd ancom2s 2ralbidva wfo
      sylan f1ofo breq2 eqeq2 breq1 cbvfo ralbidv eqeq1 bitrd anbi12d 3bitr4g
      3syl dfwe2 ) ABCDEUAZACJZFKZGKZCLZVJVKMZVKVJCLZNZGAOFAOZPBDJZHKZIKZDLZVRV
      SMZVSVRDLZNZIBOZHBOZPACQBDQVHVIVQVPWEABCDEUBVHVPVJERZVKERZDLZWFWGMZWGWFDL
      ZNZGAOZFAOZWEVHVOWKFGAAVHVJAUCZVKAUCZPZPZVLWHVMWIVNWJABVJVKCDEUDWQWIVMVHA
      BEUEZWPWIVMSVHABEUFZWRABCDEUGZABEUHUIABVJVKEUJUOUKVHWOWNVNWJSABVKVJCDEUDU
      LTUMVHWSABEUNZWMWESWTABEUPXAWMWFVSDLZWFVSMZVSWFDLZNZIBOZFAOWEXAWLXFFAWKXE
      GIABEWGVSMWHXBWIXCWJXDWGVSWFDUQWGVSWFURWGVSWFDUSTUTVAXFWDFHABEWFVRMZXEWCI
      BXGXBVTXCWAXDWBWFVRVSDUSWFVRVSVBWFVRVSDUQTVAUTVCVFVCVDFGACVGHIBDVGVE $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y v u B $.  $d x y z w v u H $.
    $d x y z w v u R $.  $d v u S $.
    $( Any one-to-one onto function determines an isomorphism with an induced
       relation ` S ` .  Proposition 6.33 of [TakeutiZaring] p. 34. $)
    f1oiso $p |- ( ( H : A -1-1-onto-> B /\ S = { <. z , w >. |
     E. x e. A E. y e. A ( ( z = ( H ` x ) /\ w = ( H ` y ) ) /\ x R y ) } ) ->
                  H Isom R , S ( A , B ) ) $=
      ( vv vu cv cfv wceq wa wbr wrex wb wcel anbi1d wf1o copab wral wiso simpl
      wf1 f1of1 cop df-br eleq2 fvex eqeq1 2rexbidv anbi2d opelopab anass eqcom
      f1fveq syl6bb anassrs syl5bb rexbidv rexbidva breq1 ceqsrexv adantl bitrd
      r19.42v breq2 sylan9bb anandis sylan9bbr syl5rbb ralrimdv ralrimiv df-iso
      an32s exp32 sylan sylanbrc ) EFIUAZHCLZALZIMZNZDLZBLZIMZNZOZWCWGGPZOZBEQA
      EQZCDUBZNZOWAJLZKLZGPZWPIMZWQIMZHPZRZKEUCZJEUCZEFGHIUDWAWOUEWAEFIUFZWOXDE
      FIUGXEWOOZXCJEXFWPESZXBKEXFXGWQESZXBXAWSWTUHZHSZXFXGXHOZOWRWSWTHUIXEXKWOX
      JWRRWOXJXIWNSZXEXKOZWRHWNXIUJXLWSWDNZWTWHNZOZWKOZBEQZAEQZXMWRWMXNWIOZWKOZ
      BEQAEQXSCDWSWTWPIUKWQIUKWBWSNZWLYAABEEYBWJXTWKYBWEXNWIWBWSWDULTTUMWFWTNZY
      AXQABEEYCXTXPWKYCWIXOXNWFWTWHULUNTUMUOXEXGXHXSWRRXEXGOZXSXOWPWGGPZOZBEQZX
      EXHOZWRYDXSWCWPNZXOWKOZBEQZOZAEQZYGYDXRYLAEYDWCESZOZXRYIYJOZBEQYLYOXQYPBE
      XQXNYJOYOYPXNXOWKUPYOXNYIYJXEXGYNXNYIRXEXGYNOOXNWPWCNYIEFWPWCIURWPWCUQUSU
      TTVAVBYIYJBEVHUSVCXGYMYGRXEYKYGAWPEYIYJYFBEYIWKYEXOWCWPWGGVDUNVBVEVFVGYHY
      GWGWQNZYEOZBEQZWRYHYFYRBEYHWGESZOXOYQYEXEXHYTXOYQRXEXHYTOOXOWQWGNYQEFWQWG
      IURWQWGUQUSUTTVCXHYSWRRXEYEWRBWQEWGWQWPGVIVEVFVGVJVKVAVLVQVMVRVNVOVSJKEFG
      HIVPVT $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d H w x y z $.  $d R w x y z $.
    f1oiso2.1 $e |- S = { <. x , y >. |
      ( ( x e. B /\ y e. B ) /\ ( `' H ` x ) R ( `' H ` y ) ) } $.
    $( Any one-to-one onto function determines an isomorphism with an induced
       relation ` S ` .  (Contributed by Mario Carneiro, 9-Mar-2013.) $)
    f1oiso2 $p |- ( H : A -1-1-onto-> B -> H Isom R , S ( A , B ) ) $=
      ( vz vw cv cfv wceq wa wbr wrex wcel 3adant3 eqcomd syl2anc wf1o wiso w3a
      copab ccnv f1ocnvdm adantrr f1ocnvfv2 jca simp3 fveq2 eqeq2d anbi2d breq2
      adantrl anbi12d rcla4ev syl12anc anbi1d breq1 rexbidv 3expib simp1 simp2l
      simp3ll f1of ffvelrn sylan eqeltrd simp3lr simp2r simp3r f1ocnvfv 3brtr4d
      wf wi mpd jca31 3exp rexlimdvv impbid opabbidv syl5eq f1oiso mpdan ) CDGU
      AZFAKZIKZGLZMZBKZJKZGLZMZNZWHWLEOZNZJCPZICPZABUDZMCDEFGUBWFFWGDQZWKDQZNZW
      GGUEZLZWKXDLZEOZNZABUDWTHWFXHWSABWFXHWSWFXCXGWSWFXCXGUCZXECQZWGXEGLZMZWNN
      ZXEWLEOZNZJCPZWSWFXCXJXGWFXAXJXBCDWGGUFUGRXIXFCQZXLWKXFGLZMZNZXGXPWFXCXQX
      GWFXBXQXACDWKGUFUORWFXCXTXGWFXCNXLXSWFXAXLXBWFXANXKWGCDWGGUHSUGWFXBXSXAWF
      XBNXRWKCDWKGUHSUOUIRWFXCXGUJXOXTXGNJXFCWLXFMZXMXTXNXGYAWNXSXLYAWMXRWKWLXF
      GUKULUMWLXFXEEUNUPUQURWRXPIXECWHXEMZWQXOJCYBWOXMWPXNYBWJXLWNYBWIXKWGWHXEG
      UKULUSWHXEWLEUTUPVAUQTVBWFWQXHIJCCWFWHCQZWLCQZNZWQXHWFYEWQUCZXAXBXGYFWGWI
      DWJWNWPWFYEVEZYFWFYCWIDQZWFYEWQVCZWFYCYDWQVDZWFCDGVOZYCYHCDGVFZCDWHGVGVHT
      VIYFWKWMDWJWNWPWFYEVJZYFWFYDWMDQZYIWFYCYDWQVKZWFYKYDYNYLCDWLGVGVHTVIYFWHW
      LXEXFEWFYEWOWPVLYFWIWGMZXEWHMZYFWGWIYGSYFWFYCYPYQVPYIYJCDWHWGGVMTVQYFWMWK
      MZXFWLMZYFWKWMYMSYFWFYDYRYSVPYIYOCDWLWKGVMTVQVNVRVSVTWAWBWCIJABCDEFGWDWE
      $.
      $( [9-Mar-2013] $)
  $}

  ${
    $d z w R $.  $d x y z w S $.  $d z w A $.  $d z w B $.  $d x y z w F $.
    f1owe.1 $e |- R = { <. x , y >. | ( F ` x ) S ( F ` y ) } $.
    $( Well-ordering of isomorphic relations. $)
    f1owe $p |- ( F : A -1-1-onto-> B -> ( S We B -> R We A ) ) $=
      ( vz vw wf1o wwe cv wbr cfv wb wral wceq fveq2 breq1d breq2d brabg rgen2a
      wa wiso df-iso isowe sylbir mpan2 biimprd ) CDGKZCELZDFLZUKIMZJMZENUNGOZU
      OGOZFNZPZJCQICQZULUMPZUSIJCAMZGOZBMZGOZFNUPVEFNURABUNUOCCEVBUNRVCUPVEFVBU
      NGSTVDUORVEUQUPFVDUOGSUAHUBUCUKUTUDCDEFGUEVAIJCDEFGUFCDEFGUGUHUIUJ $.
      $( [4-Mar-1997] $)
  $}

  ${
    $d z w v u f R $.  $d x y z w v u f S $.  $d z w v u f A $.
    $d z w v u f B $.  $d x y z w v u f F $.
    f1oweALT.1 $e |- R = { <. x , y >. | ( F ` x ) S ( F ` y ) } $.
    $( Well-ordering of isomorphic relations.  (This version is proved directly
       instead of wit the isomorphism predicate.) $)
    f1oweALT $p |- ( F : A -1-1-onto-> B -> ( S We B -> R We A ) ) $=
      ( vu vf vw vv cv wbr wceq wral wa wi wcel cfv vz wf1o wfr w3o wwe wfo wfn
      f1ofo crn df-fo freq2 biimprd wfun cdm df-fn wss c0 wne wn wrex wal df-fr
      cima cvv vex funimaex wex n0 funfvima2 ne0i syl6 exlimdv syl5bi imp jctil
      imassrn sseq1 neeq1 anbi12d raleq rexeqbi1dv imbi12d syl7 syl com23 exp3a
      cla4gv anabsi5 imp3a cres fores fvres breqan12rd fveq2 breq1d breq2d brab
      syl6rbbr ralbidva rexbiia breq1 cbvfo rexbidv breq2 ralbidv cbvexfo bitrd
      wb notbid syl5bb sylibrd exp4b com34 imp4a alrimdv syl6ibr biimpd sylan9r
      sylan9 sylbi wf1 df-f1o a1i f1fveq bicomd 3orbi123d 2ralbidva eqeq1 eqeq2
      sylan9bb anim12d dfwe2 3imtr4g ) CDGUBZDFUCZIMZJMZFNZYPYQOZYQYPFNZUDZJDPZ
      IDPZQCEUCZKMZLMZENZUUEUUFOZUUFUUEENZUDZLCPKCPZQDFUECEUEYNYOUUDUUCUUKYNCDG
      UFZYOUUDRZCDGUHUULGCUGZGUIZDOZQUUMCDGUJUUPYOUUOFUCZUUNUUDUUPUUQYOUUODFUKU
      LUUNGUMZGUNZCOZQUUQUUDRGCUOUURUUQUUSEUCZUUTUUDUURUUQUAMZUUSUPZUVBUQURZQUU
      IUSZLUVBPZKUVBUTZRZUAVAUVAUURUUQUVHUAUURUUQUVCUVDUVGUURUVCUUQUVDUVGRUURUV
      CUVDUUQUVGUURUVCUVDUUQUVGUURUVCQZUVDUUQQYTUSZJGUVBVCZPZIUVKUTZUVGUVIUVDUU
      QUVMUURUVCUVDUUQUVMRZRUURUVIUVDUVNUURUUQUVIUVDQZUVMUUQUUEUUOUPZUUEUQURZQZ
      UVJJUUEPZIUUEUTZRZKVAZUURUVOUVMRZKIJUUOFVBUURUVKVDSZUWBUWCRGUVBUAVEVFUVOU
      VKUUOUPZUVKUQURZQZUWDUWBUVMUVOUWFUWEUVIUVDUWFUVDUUEUVBSZKVGUVIUWFKUVBVHUV
      IUWHUWFKUVIUWHUUEGTZUVKSUWFUVBUUEGVIUVKUWIVJVKVLVMVNGUVBVPVOUWAUWGUVMRKUV
      KVDUUEUVKOZUVRUWGUVTUVMUWJUVPUWEUVQUWFUUEUVKUUOVQUUEUVKUQVRVSUVSUVLIUUEUV
      KUVJJUUEUVKVTWAWBWGWCWDVMWEWFWHWIUVIUVBUVKGUVBWJZUFZUVGUVMXHUVBGWKUVGUUFU
      WKTZUUEUWKTZFNZUSZLUVBPZKUVBUTZUWLUVMUVFUWQKUVBUWHUVEUWPLUVBUWHUUFUVBSZQZ
      UUIUWOUWTUWOUUFGTZUWIFNZUUIUWSUWHUWMUXAUWNUWIFUUFUVBGWLUUEUVBGWLWMAMZGTZB
      MZGTZFNZUXAUXFFNUXBABUUFUUEELVEZKVEZUXCUUFOUXDUXAUXFFUXCUUFGWNWOUXEUUEOUX
      FUWIUXAFUXEUUEGWNWPHWQZWRXIWSWTUWLUWRYQUWNFNZUSZJUVKPZKUVBUTUVMUWLUWQUXMK
      UVBUWPUXLLJUVBUVKUWKUWMYQOUWOUXKUWMYQUWNFXAXIXBXCUXMUVLKIUVBUVKUWKUWNYPOZ
      UXLUVJJUVKUXNUXKYTUWNYPYQFXDXIXEXFXGXJWDXKXLXMWEXNXOUAKLUUSEVBXPUUTUVAUUD
      UUSCEUKXQXSXTXRXTWDYNUUKUUCYNCDGYAZUULQUUKUUCXHCDGYBUXOUUKUWIUXAFNZUWIUXA
      OZUXBUDZLCPZKCPZUULUUCUXOUUJUXRKLCCUXOUUECSUUFCSQQZUUGUXPUUHUXQUUIUXBUUGU
      XPXHUYAUXGUWIUXFFNUXPABUUEUUFEUXIUXHUXCUUEOUXDUWIUXFFUXCUUEGWNWOUXEUUFOUX
      FUXAUWIFUXEUUFGWNWPHWQYCUYAUXQUUHCDUUEUUFGYDYEUUIUXBXHUYAUXJYCYFYGUULUXTY
      PUXAFNZYPUXAOZUXAYPFNZUDZLCPZIDPUUCUXSUYFKICDGUWIYPOZUXRUYELCUYGUXPUYBUXQ
      UYCUXBUYDUWIYPUXAFXAUWIYPUXAYHUWIYPUXAFXDYFXEXBUULUYFUUBIDUYEUUALJCDGUXAY
      QOUYBYRUYCYSUYDYTUXAYQYPFXDUXAYQYPYIUXAYQYPFXAYFXBXEXGYJXTULYKIJDFYLKLCEY
      LYM $.
      $( [4-Mar-1997] $)
  $}


