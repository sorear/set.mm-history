$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Add new constant symbol. $)
  $c sup $.  $( Supremum. $)

  $( Extend class notation to include supremum of class ` A ` .  Here ` R `
     is ordinarily a relation that strictly orders class ` B ` .  For example,
     ` R ` could be 'less than' and ` B ` could be the set of real numbers. $)
  csup $a class sup ( A , B , R ) $.

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Define the supremum of class ` B ` .  It is meaningful when ` R `
       is a relation that strictly orders ` A ` and when the supremum
       exists.  For example, ` R ` could be 'less than', ` A ` could be the
       set of real numbers, and ` B ` could be the set of all positive
       reals whose square is less than 2; in this case the supremum is defined
       as the square root of 2 per ~ sqrval .

       We will also use this notation for "infimum" by replacing ` R ` with
       ` ``' R ` . $)
    df-sup $a |- sup ( B , A , R ) = U. { x e. A | ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) } $.

    $( can't be used if A is a proper class... important?  we already
       have U. as sup for On... but since sup exists, can't use sup for
       On anyway e.g. sup of On itself.
    df-supNEW $a |- sup = { <. <. x , y >. , z >. | z =
             U. { w e. ( 1st ` x ) | ( A. v e. y -. w ( 2nd ` x ) v /\
                  A. v e. ( 1st ` x ) ( v ( 2nd ` x ) w ->
                 E. u e. y v ( 2nd ` x ) u ) ) } } $.
    $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.

    $( Equality theorem for supremum. $)
    supeq1 $p |- ( B = C -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup raleq1 rexeq1
      imbi2d ralbidv anbi12d rabbisdv unieqd df-sup 3eqtr4g ) BCHZEIZFIZDJKZFBL
      ZUJUIDJZUJGIDJZGBMZNZFALZOZEAPZQUKFCLZUMUNGCMZNZFALZOZEAPZQBADRCADRUHUSVE
      UHURVDEAUHULUTUQVCUKFBCSUHUPVBFAUHUOVAUMUNGBCTUAUBUCUDUEEFGABDUFEFGACDUFU
      G $.
      $( [22-May-1999] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    supmo.1 $e |- R Or A $.
    $( Any class ` B ` has at most one supremum in ` A ` (where ` R ` is
       interpreted as 'less than'). $)
    supmo $p |- E* x ( x e. A /\ ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( vw cv wcel wbr wn wral wrex wi wa wmo wceq wal eleq1 breq1 negbid
      ralbidv breq2 imbi1d anbi12d mo4 rexbidv imbi12d rcla4va con3d imp
      cbvralv ralnex bitr sylan2b an1rs adantlrr adantrl rcla4cva anasss
      adantrrr adantll jca wb wor sotrieq2 mpan ad2ant2r mpbird ax-gen mpgbir
      ) AIZDJZVMBIZFKZLZBEMZVOVMFKZVOCIZFKZCENZOZBDMZPZPZAQWFHIZDJZWGVOFKZLZBEM
      ZVOWGFKZWBOZBDMZPZPZPZVMWGRZOZHSAWFWPAHWRVNWHWEWOVMWGDTWRVRWKWDWNWRVQWJBE
      WRVPWIVMWGVOFUAUBUCWRWCWMBDWRVSWLWBVMWGVOFUDUEUCUFUFUGWSHWQWRVMWGFKZLZWGV
      MFKZLZPZWQXAXCWFWOXAWHWFWNXAWKVNVRWNXAWDVNWNVRXAVNWNPZVMVTFKZCENZLZXAVRXE
      XHXAXEWTXGWMWTXGOBVMDVOVMRZWLWTWBXGVOVMWGFUAXIWAXFCEVOVMVTFUAUHUIUJUKULVR
      XFLZCEMXHVQXJBCEVOVTRZVPXFVOVTVMFUDUBUMXFCEUNUOUPUQURUSUSWEWPXCVNWDWPXCVR
      WDWHWKXCWNWDWHWKXCWDWHPZWGVTFKZCENZLZXCWKXLXOXCXLXBXNWCXBXNOBWGDVOWGRZVSX
      BWBXNVOWGVMFUAXPWAXMCEVOWGVTFUAUHUIUTUKULWKXMLZCEMXOWJXQBCEXKWIXMVOVTWGFU
      DUBUMXMCEUNUOUPVAVBVCVCVDVNWHWRXDVEZWEWODFVFVNWHPXRGDVMWGFVGVHVIVJVKVL $.
      $( [5-May-1999] $)

    $( A supremum is a set. $)
    supex $p |- sup ( B , A , R ) e. V $=
      ( vx vy vz csup cv wcel wbr wn wral wrex wi wa cab cuni cvv crab df-sup
      df-rab unieqi eqtr wmo supmo moabex ax-mp uniex eqeltr ) BACHZEIZAJULFIZC
      KLFBMUMULCKUMGICKGBNOFAMPZPZEQZRZSUKUNEATZRUQEFGABCUAURUPUNEAUBUCUDUPUOEU
      EUPSJEFGABCDUFUOEUGUHUIUJ $.
      $( [22-May-1999] $)

    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeu $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                  E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( cv wbr wn wral wrex wi wa wcel wmo wreu supmo jctr reu5 sylibr ) AHZBHZ
      FIJBEKUCUBFIUCCHFICELMBDKNZADLZUEUBDOUDNAPZNUDADQUEUFABCDEFGRSUDADTUA $.
      $( [13-Oct-2004] $) $( [12-Oct-2004] $)

    $( A supremum belongs to its base class (closure law). $)
    supcl $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                sup ( B , A , R ) e. A ) $=
      ( cv wbr wn wral wrex wi wa crab cuni csup wreu wcel supeu reucl syl
      df-sup syl5eqel ) AHZBHZFIJBEKUFUEFIUFCHFICELMBDKNZADLZUGADOPZDEDFQUHUGAD
      RUIDSABCDEFGTUGADUAUBABCDEFUCUD $.
      $( [13-Oct-2004] $) $( [12-Oct-2004] $)

    $( A supremum is an upper bound. $)
    supub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                 ( C e. B -> -. sup ( B , A , R ) R C ) ) $=
      ( vw wcel cv wbr wn wral wrex wi wa csup wceq breq2 negbid imbi2d rcla4v
      crab cuni df-sup eqcomi wreu wb breq1 ralbidv imbi1d anbi12d reuuni2
      supcl supeu sylanc mpbiri pm3.26d syl5 vtoclga com12 ) FEJAKZBKZGLZMZBENZ
      VDVCGLZVDCKGLCEOZPZBDNZQZADOZEDGRZFGLZMZVMVNIKZGLZMZPVMVPPIFEVQFSZVSVPVMV
      TVRVOVQFVNGTUAUBVQEJVNVDGLZMZBENZVSVMWBVSBVQEVDVQSWAVRVDVQVNGTUAUCVMWCVDV
      NGLZVIPZBDNZVMWCWFQZVLADUDUEZVNSZVNWHABCDEGUFUGVNDJVLADUHWGWIUIVMVLWGADVN
      VCVNSZVGWCVKWFWJVFWBBEWJVEWAVCVNVDGUJUAUKWJVJWEBDWJVHWDVIVCVNVDGTULUKUMUN
      ABCDEGHUOABCDEGHUPUQURUSUTVAVB $.
      $( [13-Oct-2004] $) $( [12-Oct-2004] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                      E. z e. B C R z ) ) $=
        ( vw cv wbr wn wral wrex wi wa wcel csup wceq breq1 rexbidv imbi12d
        imbi2d rcla4v crab cuni df-sup eqcomi wreu wb negbid ralbidv breq2
        imbi1d anbi12d reuuni2 supcl supeu sylanc mpbiri pm3.27d syl5 vtoclga
        com12 imp3a ) AJZBJZGKZLZBEMZVGVFGKZVGCJZGKZCENZOZBDMZPZADNZFDQZFEDGRZG
        KZFVLGKZCENZVSVRWAWCOZVRIJZVTGKZWEVLGKZCENZOZOVRWDOIFDWEFSZWIWDVRWJWFWA
        WHWCWEFVTGTWJWGWBCEWEFVLGTUAUBUCWEDQVGVTGKZVNOZBDMZWIVRWLWIBWEDVGWESZWK
        WFVNWHVGWEVTGTWNVMWGCEVGWEVLGTUAUBUDVRVTVGGKZLZBEMZWMVRWQWMPZVQADUEUFZV
        TSZVTWSABCDEGUGUHVTDQVQADUIWRWTUJVRVQWRADVTVFVTSZVJWQVPWMXAVIWPBEXAVHWO
        VFVTVGGTUKULXAVOWLBDXAVKWKVNVFVTVGGUMUNULUOUPABCDEGHUQABCDEGHURUSUTVAVB
        VCVDVE $.
        $( [14-Oct-2004] $) $( [13-Oct-2004] $)

      $( An upper bound is not less than the supremum. $)
      supnub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ A. z e. B -. C R z ) ->
                      -. C R sup ( B , A , R ) ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub exp3a imp dfrex2 syl6ib
        con2d ex imp3a ) AIZBIZGJKBELUGUFGJUGCIZGJCEMNBDLOADMZFDPZFUHGJZKCELZFE
        DGQGJZKZUIUJULUNNUIUJOZUMULUOUMUKCEMZULKUIUJUMUPNUIUJUMUPABCDEFGHRSTUKC
        EUAUBUCUDUE $.
        $( [14-Oct-2004] $) $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    sup.1 $e |- R Or A $.
    sup.2 $e |- E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $.
    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeui $p |- E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $=
      ( cv wbr wn wral wrex wi wa wreu supeu ax-mp ) AIZBIZFJKBELTSFJTCIFJCEMNB
      DLOZADMUAADPHABCDEFGQR $.
      $( [23-May-1999] $)

    $( A supremum belongs to its base class (closure law). $)
    supcli $p |- sup ( B , A , R ) e. A $=
      ( cv wbr wn wral wrex wi wa csup wcel supcl ax-mp ) AIZBIZFJKBELUATFJUACI
      FJCEMNBDLOADMEDFPDQHABCDEFGRS $.
      $( [23-May-1999] $)

    $( A supremum is an upper bound. $)
    supubi $p |- ( C e. B -> -. sup ( B , A , R ) R C ) $=
      ( cv wbr wn wral wrex wi wa wcel csup supub ax-mp ) AJZBJZGKLBEMUBUAGKUBC
      JGKCENOBDMPADNFEQEDGRFGKLOIABCDEFGHST $.
      $( [23-May-1999] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplubi $p |- ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                    E. z e. B C R z ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFEDGRGKPFUCGKCENOIABCDEFGHST $.
        $( [23-May-1999] $)

      $( An upper bound is not less than the supremum. $)
      supnubi $p |- ( ( C e. A /\ A. z e. B -. C R z ) ->
                   -. C R sup ( B , A , R ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup supnub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFUCGKLCEMPFEDGRGKLOIABCDEFGHST $.
        $( [23-May-1999] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    suppr.1 $e |- R Or A $.
    $( The supremum of a pair. $)
    suppr $p |- ( ( B e. A /\ C e. A ) -> sup ( { B , C } , A , R ) =
                if ( C R B , B , C ) ) $=
      ( vx vy vz wcel wa cv wbr wn cpr wral wrex wi crab cuni cif csup wceq wo
      breq2 negbid wor sonr mpan ad2antrr syl5cbir so2nr imnan sylibr ancoms
      imp jaod wb iftrue breq1d adantl sylibrd pm3.27 ad2antlr iffalse
      pm2.61dan visset elpr syl5ib r19.21aiv rcla4ev ifpr sylan ex a1d jca
      wreu breq1 ralbidv imbi1d anbi12d reuuni2 ifcl wmo sylanc supmo jctir
      reu5 mpbid df-sup syl5eq ) BAIZCAIZJZFKZGKZDLZMZGBCNZOZWOWNDLZWOHKZDLZHWR
      PZQZGAOZJZFARSZCBDLZBCTZWRADUAWMXIWODLZMZGWROZWOXIDLZXCQZGAOZJZXGXIUBZWMX
      LXOWMXKGWRWMWOBUBZWOCUBZUCZXKWOWRIWMXHXTXKQWMXHJZXTBWODLZMZXKYAXRYCXSXRYC
      BBDLZMZYAXRYBYDWOBBDUDUEWKYEWLXHADUFZWKYEEABDUGUHUIUJXSYCBCDLZMZYAXSYBYGW
      OCBDUDUEWMXHYHWLWKXHYHQZWLWKJZXHYGJMZYIYFYJYKEACBDUKUHXHYGULUMUNUOUJUPXHX
      KYCUQWMXHXJYBXHXIBWODXHBCURUSUEUTVAWMXHMZJZXTCWODLZMZXKYMXRYOXSXRYOYLYMXR
      YNXHWOBCDUDUEWMYLVBUJXSYOCCDLZMZYMXSYNYPWOCCDUDUEWLYQWKYLYFWLYQEACDUGUHVC
      UJUPYLXKYOUQWMYLXJYNYLXICWODXHBCVDUSUEUTVAVEWOBCGVFVGVHVIWMXNGAWMXNWOAIWM
      XMXCXIWRIXMXCWMXBXMHXIWRXAXIWODUDVJXHBCAAVKVLVMVNVIVOZXIAIZXFFAVPZXPXQUQW
      MXFXPFAXIWNXIUBZWSXLXEXOUUAWQXKGWRUUAWPXJWNXIWODVQUEVRUUAXDXNGAUUAWTXMXCW
      NXIWODUDVSVRVTZWAXHBCAWBZWMXFFAPZWNAIXFJFWCZJYTWMUUDUUEYSXPUUDWMXFXPFXIAU
      UBVJUUCYRWDFGHAWRDEWEWFXFFAWGUMWDWHFGHAWRDWIWJ $.
      $( [21-Jun-2007] $) $( [17-Jun-2007] $)

    $( The supremum of a singleton. $)
    supsn $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( wcel csn csup wbr cif cpr wceq suppr anidms dfsn2 supeq1 ax-mp syl5eq
      ifid syl6eq ) BAEZBFZACGZBBCHZBBIZBTBBJZACGZUDUBTUFUDKABBCDLMUAUEKUBUFKBN
      AUAUECOPQUCBRS $.
      $( [4-Oct-2007] $) $( [2-Oct-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.
    supsn.1 $e |- R Or A $.
    $( TO BE REVISED - delete this? $)
    $( The supremum of a singleton.  This version of ~ supsn is proved
       directly. $)
    supsnALT $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( vx vy vz wcel cv wbr wn csn wral wrex wi wa crab cuni csup wceq elsni
      breq2 negbid wor sonr mpan syl5bir syl com12 r19.21aiv rcla4ev snidg
      sylan ex a1d jca wreu wb wmo breq1 ralbidv imbi1d anbi12d mpdan supmo
      jctir reu5 sylibr reuuni2 mpbid df-sup syl5eq ) BAHZEIZFIZCJZKZFBLZMZVOVN
      CJZVOGIZCJZGVRNZOZFAMZPZEAQRZBVRACSVMBVOCJZKZFVRMZVOBCJZWCOZFAMZPZWGBTZVM
      WJWMVMWIFVRVOVRHZVMWIWPVOBTZVMWIOVOBUAWQWIBBCJZKZVMWQWHWRVOBBCUBUCACUDVMW
      SDABCUEUFUGUHUIUJVMWLFAVMWLVOAHVMWKWCBVRHWKWCVMWBWKGBVRWABVOCUBUKBAULUMUN
      UOUJUPZVMWFEAUQZWNWOURVMWFEANZVNAHWFPEUSZPXAVMXBXCVMWNXBWTWFWNEBAVNBTZVSW
      JWEWMXDVQWIFVRXDVPWHVNBVOCUTUCVAXDWDWLFAXDVTWKWCVNBVOCUBVBVAVCZUKVDEFGAVR
      CDVEVFWFEAVGVHWFWNEABXEVIVDVJEFGAVRCVKVL $.
      $( [23-May-1999] $)
  $}

