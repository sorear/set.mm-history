$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Add new constant symbol. $)
  $c sup $.  $( Supremum. $)

  $( Extend class notation to include supremum of class ` A ` .  Here ` R ` is
     ordinarily a relation that strictly orders class ` B ` .  For example,
     ` R ` could be 'less than' and ` B ` could be the set of real numbers. $)
  csup $a class sup ( A , B , R ) $.

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Define the supremum of class ` B ` .  It is meaningful when ` R ` is a
       relation that strictly orders ` A ` and when the supremum exists.  For
       example, ` R ` could be 'less than', ` A ` could be the set of real
       numbers, and ` B ` could be the set of all positive reals whose square
       is less than 2; in this case the supremum is defined as the square root
       of 2 per ~ sqrval .  See ~ dfsup2 for alternate definition not requiring
       dummy variables.

       We will also use this notation for "infimum" by replacing ` R ` with
       ` ``' R ` . $)
    df-sup $a |- sup ( B , A , R ) = U. { x e. A | ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) } $.

    $( can't be used if A is a proper class... important?  we already
       have U. as sup for On... but since sup exists, can't use sup for
       On anyway e.g. sup of On itself.
    df-supNEW $a |- sup = { <. <. x , y >. , z >. | z =
             U. { w e. ( 1st ` x ) | ( A. v e. y -. w ( 2nd ` x ) v /\
                  A. v e. ( 1st ` x ) ( v ( 2nd ` x ) w ->
                 E. u e. y v ( 2nd ` x ) u ) ) } } $.
    $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.
    $( Quantifier free definition of supremum.  (Contributed by Scott Fenton,
       19-Feb-2013.) $)
    dfsup2 $p |- sup ( B , A , R ) =
      U. ( A \ ( ( `' R " B ) u. ( R " ( A \ ( `' R " B ) ) ) ) ) $=
      ( vx vy vz csup cv wbr wn wral wrex wi wa crab cuni ccnv cima cdif cun
      df-sup cab cin cvv dfrab2 incom eqtri wceq wcel wb abeq1 eldif visset
      mpbiran wo elima dfrex2 bitri orbi12i elun ianor 3bitr4i con2bii brcnv
      notbii ralbii impexp imbi1i rexbii imbi2i con34b bitr3i ralbii2 anbi12i
      3bitr2ri mpgbir ineq2i invdif 3eqtri unieqi ) BACGDHZEHZCIZJZEBKZWBWACIZW
      BFHZCIZFBLZMZEAKZNZDAOZPACQZBRZCAWOSZRZTZSZPDEFABCUAWMWSWMAWLDUBZUCZAUDWR
      SZUCWSWMWTAUCXAWLDAUEWTAUFUGWTXBAWTXBUHWLWAXBUIZUJDWLDXBUKXCWAWRUIZJZWBWA
      WNIZJZEBKZWFJZEWPKZNZWLXCWAUDUIXEWAUDWRULDUMZUNXDXKWAWOUIZWAWQUIZUOXHJZXJ
      JZUOXDXKJXMXOXNXPXMXFEBLXOEWAWNBXLUPXFEBUQURXNWFEWPLXPEWACWPXLUPWFEWPUQUR
      USWAWOWQUTXHXJVAVBVCXHWEXJWKXGWDEBXFWCWBWACEUMZXLVDVEVFXIWJEWPAWBAUIZWBWO
      UIZJZNZXIMXRXTXIMZMWBWPUIZXIMXRWJMXRXTXIVGYCYAXIWBAWOULVHWJYBXRWJWFXSMYBX
      SWIWFXSWGWBWNIZFBLWIFWBWNBXQUPYDWHFBWGWBCFUMXQVDVIURVJWFXSVKVLVJVBVMVNVOV
      PVQAWRVRVSVTUG $.
      $( [21-Feb-2013] $) $( [19-Feb-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.
    $( Quantifier-free definition of supremum.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    dfsup2OLD $p |- sup ( B , A , R ) =
      U. ( A \ ( ( `' R " B ) u. ( ( R \ ( ( `' R " B ) X. _V ) ) " A ) ) ) $=
      ( vx vy vz csup cv wbr wn wral wrex wi wa crab cuni ccnv cima cvv cxp
      cdif cun df-sup cab cin dfrab2 incom wceq wcel wb abeq1 eldif visset
      mpbiran wo elun notbii ioran bitri elima brcnv rexbii ralnex bitr4i
      brdif brxp mpbiran2 3bitri anbi2i rexanali con2bii anbi12i 3bitrri
      mpgbir ineq2i invdif eqtri 3eqtri unieqi ) BACGDHZEHZCIZJEBKZWAVTCIZWAFHZ
      CIZFBLZMEAKZNZDAOZPACQZBRZCWLSTZUAZARZUBZUAZPDEFABCUCWJWQWJWIDUDZAUEAWRUE
      ZWQWIDAUFWRAUGWSASWPUAZUEWQWRWTAWRWTUHWIVTWTUIZUJDWIDWTUKXAVTWPUIZJZVTWLU
      IZJZVTWOUIZJZNZWIXAVTSUIZXCVTSWPULDUMZUNXCXDXFUOZJXHXBXKVTWLWOUPUQXDXFURU
      SXEWCXGWHXEWBEBLZJWCXDXLXDWAVTWKIZEBLXLEVTWKBXJUTXMWBEBWAVTCEUMZXJVAVBUSU
      QWBEBVCVDXGWDWGJZNZEALZJWHXFXQXFWAVTWNIZEALXQEVTWNAXJUTXRXPEAXRWDWAVTWMIZ
      JZNXPWAVTCWMVEXTXOWDXSWGXSWAWLUIZWEWAWKIZFBLWGXSYAXIWAVTWLSXJVFXJVGFWAWKB
      XNUTYBWFFBWEWACFUMXNVAVBVHUQVIUSVBUSUQXQWHWDWGEAVJVKVDVLVMVNVOAWPVPVQVRVS
      VQ $.
      $( [18-Feb-2013] $)

  $}

  $( Quantifier-free definition of supremum.  (Contributed by Scott Fenton,
     19-Feb-2013.) $)
  dfsup3OLD $p |- sup ( B , A , R ) =
    U. ( A \ ( ( `' R " B ) u. ( R " ( A \ ( `' R " B ) ) ) ) ) $=
    ( csup ccnv cima cvv cxp cdif cun cuni dfsup2OLD cres crn cin indifcom
    incom difxp1 ineq2i 3eqtr4i df-res rneqi df-ima uneq2i difeq2i unieqi
    eqtri ) BACDACEBFZCUHGHZIZAFZJZIZKAUHCAUHIZFZJZIZKABCLUMUQULUPAUKUOUHUJAMZN
    CUNMZNUKUOURUSUJAGHZOZCUNGHZOZURUSUTUJOCUTUIIZOVAVCUTCUIPUJUTQVBVDCAUHGRSTU
    JAUACUNUATUBUJAUCCUNUCTUDUEUFUG $.
    $( [19-Feb-2013] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Equality theorem for supremum. $)
    supeq1 $p |- ( B = C -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup raleq rexeq
      imbi2d ralbidv anbi12d rabbidv unieqd df-sup 3eqtr4g ) BCHZEIZFIZDJKZFBLZ
      UJUIDJZUJGIDJZGBMZNZFALZOZEAPZQUKFCLZUMUNGCMZNZFALZOZEAPZQBADRCADRUHUSVEU
      HURVDEAUHULUTUQVCUKFBCSUHUPVBFAUHUOVAUMUNGBCTUAUBUCUDUEEFGABDUFEFGACDUFUG
      $.
      $( [22-May-1999] $)
  $}

  ${
    supeq1d.1 $e |- ( ph -> B = C ) $.
    $( Equality deduction for supremum.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    supeq1d $p |- ( ph -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( wceq csup supeq1 syl ) ACDGCBEHDBEHGFBCDEIJ $.
      $( [22-Jun-2011] $)
  $}

  ${
    supeq1i.1 $e |- B = C $.
    $( Equality inference for supremum.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    supeq1i $p |- sup ( B , A , R ) = sup ( C , A , R ) $=
      ( wceq csup supeq1 ax-mp ) BCFBADGCADGFEABCDHI $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    supmo.1 $e |- R Or A $.
    $( Any class ` B ` has at most one supremum in ` A ` (where ` R ` is
       interpreted as 'less than'). $)
    supmo $p |- E* x ( x e. A /\ ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( vw cv wcel wbr wn wral wrex wi wa wmo wceq wal eleq1 breq1 notbid
      ralbidv breq2 imbi1d anbi12d mo4 wb wor sotrieq2 mpan ad2ant2r cbvralv
      ralnex bitri rexbidv imbi12d rcla4va con3d imp sylan2b an32s adantlrr
      adantrl rcla4cva anasss adantrrr adantll mpbir2and ax-gen mpgbir ) AIZDJZ
      VLBIZFKZLZBEMZVNVLFKZVNCIZFKZCENZOZBDMZPZPZAQWEHIZDJZWFVNFKZLZBEMZVNWFFKZ
      WAOZBDMZPZPZPZVLWFRZOZHSAWEWOAHWQVMWGWDWNVLWFDTWQVQWJWCWMWQVPWIBEWQVOWHVL
      WFVNFUAUBUCWQWBWLBDWQVRWKWAVLWFVNFUDUEUCUFUFUGWRHWPWQVLWFFKZLZWFVLFKZLZVM
      WGWQWTXBPUHZWDWNDFUIVMWGPXCGDVLWFFUJUKULWEWNWTWGWEWMWTWJVMVQWMWTWCVMWMVQW
      TVQVMWMPZVLVSFKZCENZLZWTVQXELZCEMXGVPXHBCEVNVSRZVOXEVNVSVLFUDUBUMXECEUNUO
      XDXGWTXDWSXFWLWSXFOBVLDVNVLRZWKWSWAXFVNVLWFFUAXJVTXECEVNVLVSFUAUPUQURUSUT
      VAVBVCVDVDWDWOXBVMWCWOXBVQWCWGWJXBWMWCWGWJXBWJWCWGPZWFVSFKZCENZLZXBWJXLLZ
      CEMXNWIXOBCEXIWHXLVNVSWFFUDUBUMXLCEUNUOXKXNXBXKXAXMWBXAXMOBWFDVNWFRZVRXAW
      AXMVNWFVLFUAXPVTXLCEVNWFVSFUAUPUQVEUSUTVAVFVGVHVHVIVJVK $.
      $( [5-May-1999] $)

    $( A supremum is a set. $)
    supex $p |- sup ( B , A , R ) e. _V $=
      ( vx vy vz csup cv wcel wbr wn wral wrex wi wa cab cuni cvv crab df-sup
      df-rab unieqi eqtri wmo supmo moabex ax-mp uniex eqeltri ) BACHZEIZAJULFI
      ZCKLFBMUMULCKUMGICKGBNOFAMPZPZEQZRZSUKUNEATZRUQEFGABCUAURUPUNEAUBUCUDUPUO
      EUEUPSJEFGABCDUFUOEUGUHUIUJ $.
      $( [22-May-1999] $)

    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeu $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                  E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( cv wbr wn wral wrex wi wa wcel wmo wreu supmo jctr reu5 sylibr ) AHZBHZ
      FIJBEKUCUBFIUCCHFICELMBDKNZADLZUEUBDOUDNAPZNUDADQUEUFABCDEFGRSUDADTUA $.
      $( [12-Oct-2004] $)

    $( A supremum belongs to its base class (closure law). $)
    supcl $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                sup ( B , A , R ) e. A ) $=
      ( cv wbr wn wral wrex wi wa csup crab cuni df-sup wreu wcel supeu reucl
      syl syl5eqel ) AHZBHZFIJBEKUFUEFIUFCHFICELMBDKNZADLZEDFOUGADPQZDABCDEFRUH
      UGADSUIDTABCDEFGUAUGADUBUCUD $.
      $( [12-Oct-2004] $)

    $( A supremum is an upper bound. $)
    supub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                 ( C e. B -> -. sup ( B , A , R ) R C ) ) $=
      ( vw wcel cv wbr wn wral wrex wi wa csup wceq breq2 notbid imbi2d crab
      cuni df-sup eqcomi wreu wb supcl supeu breq1 ralbidv imbi1d anbi12d
      reuuni2 syl11anc mpbiri simpld rcla4v syl5 vtoclga com12 ) FEJAKZBKZGLZMZ
      BENZVDVCGLZVDCKGLCEOZPZBDNZQZADOZEDGRZFGLZMZVMVNIKZGLZMZPVMVPPIFEVQFSZVSV
      PVMVTVRVOVQFVNGTUAUBVMVNVDGLZMZBENZVQEJVSVMWCVDVNGLZVIPZBDNZVMWCWFQZVLADU
      CUDZVNSZVNWHABCDEGUEUFVMVNDJVLADUGWGWIUHABCDEGHUIABCDEGHUJVLWGADVNVCVNSZV
      GWCVKWFWJVFWBBEWJVEWAVCVNVDGUKUAULWJVJWEBDWJVHWDVIVCVNVDGTUMULUNUOUPUQURW
      BVSBVQEVDVQSWAVRVDVQVNGTUAUSUTVAVB $.
      $( [12-Oct-2004] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                      E. z e. B C R z ) ) $=
        ( vw cv wbr wn wral wrex wi wa wcel csup wceq breq1 rexbidv imbi12d
        imbi2d crab cuni df-sup eqcomi wreu wb supcl supeu notbid ralbidv
        breq2 imbi1d anbi12d reuuni2 syl11anc mpbiri simprd rcla4v syl5
        vtoclga com12 imp3a ) AJZBJZGKZLZBEMZVGVFGKZVGCJZGKZCENZOZBDMZPZADNZFDQ
        ZFEDGRZGKZFVLGKZCENZVSVRWAWCOZVRIJZVTGKZWEVLGKZCENZOZOVRWDOIFDWEFSZWIWD
        VRWJWFWAWHWCWEFVTGTWJWGWBCEWEFVLGTUAUBUCVRVGVTGKZVNOZBDMZWEDQWIVRVTVGGK
        ZLZBEMZWMVRWPWMPZVQADUDUEZVTSZVTWRABCDEGUFUGVRVTDQVQADUHWQWSUIABCDEGHUJ
        ABCDEGHUKVQWQADVTVFVTSZVJWPVPWMWTVIWOBEWTVHWNVFVTVGGTULUMWTVOWLBDWTVKWK
        VNVFVTVGGUNUOUMUPUQURUSUTWLWIBWEDVGWESZWKWFVNWHVGWEVTGTXAVMWGCEVGWEVLGT
        UAUBVAVBVCVDVE $.
        $( [13-Oct-2004] $)

      $( An upper bound is not less than the supremum. $)
      supnub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ A. z e. B -. C R z ) ->
                      -. C R sup ( B , A , R ) ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub expdimp dfrex2 syl6ib
        con2d expimpd ) AIZBIZGJKBELUEUDGJUECIZGJCEMNBDLOADMZFDPZFUFGJZKCELZFED
        GQGJZKUGUHOZUKUJULUKUICEMZUJKUGUHUKUMABCDEFGHRSUICETUAUBUC $.
        $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    sup.1 $e |- R Or A $.
    sup.2 $e |- E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $.
    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeui $p |- E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $=
      ( cv wbr wn wral wrex wi wa wreu supeu ax-mp ) AIZBIZFJKBELTSFJTCIFJCEMNB
      DLOZADMUAADPHABCDEFGQR $.
      $( [23-May-1999] $)

    $( A supremum belongs to its base class (closure law). $)
    supcli $p |- sup ( B , A , R ) e. A $=
      ( cv wbr wn wral wrex wi wa csup wcel supcl ax-mp ) AIZBIZFJKBELUATFJUACI
      FJCEMNBDLOADMEDFPDQHABCDEFGRS $.
      $( [23-May-1999] $)

    $( A supremum is an upper bound. $)
    supubi $p |- ( C e. B -> -. sup ( B , A , R ) R C ) $=
      ( cv wbr wn wral wrex wi wa wcel csup supub ax-mp ) AJZBJZGKLBEMUBUAGKUBC
      JGKCENOBDMPADNFEQEDGRFGKLOIABCDEFGHST $.
      $( [23-May-1999] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplubi $p |- ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                    E. z e. B C R z ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFEDGRGKPFUCGKCENOIABCDEFGHST $.
        $( [23-May-1999] $)

      $( An upper bound is not less than the supremum. $)
      supnubi $p |- ( ( C e. A /\ A. z e. B -. C R z ) ->
                   -. C R sup ( B , A , R ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup supnub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFUCGKLCEMPFEDGRGKLOIABCDEFGHST $.
        $( [23-May-1999] $)
    $}
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( A set that contains a greatest element satisfies the antecedent in
       supremum theorems.  This allows ` sup ( A , B , R ) ` to be used in some
       situations without the completeness axiom.  (Contributed by Jeff
       Hoffman, 17-Jun-2008.) $)
    supmaxlem $p |- ( ( C e. A /\ C e. B /\ A. z e. B -. C R z ) -> E. x e. A
         ( A. y e. B -. x R y /\ A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( wcel cv wbr wn wral wrex wi wa wceq breq2 notbid cbvralv biimpi
      rcla4ev ex a1d r19.21aiv anim12i ancoms breq1 ralbidv imbi1d anbi12d
      sylan2 3impb ) FDHZFEHZFCIZGJZKZCELZAIZBIZGJZKZBELZUTUSGJZUTUOGJZCEMZNZBD
      LZOZADMZUNUROUMFUTGJZKZBELZUTFGJZVFNZBDLZOZVJURUNVQURVMUNVPURVMUQVLCBEUOU
      TPUPVKUOUTFGQRSTUNVOBDUNVOUTDHUNVNVFVEVNCFEUOFUTGQUAUBUCUDUEUFVIVQAFDUSFP
      ZVCVMVHVPVRVBVLBEVRVAVKUSFUTGUGRUHVRVGVOBDVRVDVNVFUSFUTGQUIUHUJUAUKUL $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    supmax.1 $e |- R Or A $.
    $( The greatest element of a set is its supremum.  Note that the converse
       is not true; the supremum might not be an element of the set
       considered.  (Contributed by Jeff Hoffman, 17-Jun-2008.) $)
    supmax $p |- ( ( C e. A /\ C e. B /\ A. y e. B -. C R y )
                           -> sup ( B , A , R ) = C ) $=
      ( vx vz wcel cv wbr wn wral w3a csup wceq wa wb wrex wi supmaxlem supcl
      syl simp1 wor sotrieq2 mpan syl11anc simp2 supub sylc 3simpb supnub
      mpbir2and ) DBIZDCIZDAJZEKLACMZNZCBEOZDPZUTDEKLZDUTEKLZUSUTBIZUOVAVBVCQRZ
      USGJZHJZEKLHCMVGVFEKVGUQEKACSTHBMQGBSZVDGHABCDEUAZGHABCEFUBUCUOUPURUDBEUE
      VDUOQVEFBUTDEUFUGUHUSVHUPVBVIUOUPURUIGHABCDEFUJUKUSVHUOURQVCVIUOUPURULGHA
      BCDEFUMUKUN $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    suppr.1 $e |- R Or A $.
    $( The supremum of a pair. $)
    suppr $p |- ( ( B e. A /\ C e. A ) -> sup ( { B , C } , A , R ) =
                if ( C R B , B , C ) ) $=
      ( vx vy vz wcel wa cpr csup cv wbr wn wral wrex wi crab cuni cif df-sup
      wceq wreu wb ifcl wmo wo visset elpr wor sonr mpan ad2antrr breq2 notbid
      syl5ibrcom so2nr imnan sylibr ancoms imp jaod iftrue breq1d adantl
      sylibrd simpr ad2antlr iffalse pm2.61dan syl5bi r19.21aiv ifpr rcla4ev
      sylan ex a1d breq1 ralbidv imbi1d anbi12d syl12anc supmo jctir reu5
      reuuni2 syl11anc mpbi2and syl5eq ) BAIZCAIZJZBCKZADLFMZGMZDNZOZGWNPZWPWOD
      NZWPHMZDNZHWNQZRZGAPZJZFASTZCBDNZBCUAZFGHAWNDUBWMXIWPDNZOZGWNPZWPXIDNZXCR
      ZGAPZXGXIUCZWMXIAIZXFFAUDZXLXOJZXPUEXHBCAUFZWMXFFAQZWOAIXFJFUGZJXRWMYAYBW
      MXQXLXOYAXTWMXKGWNWPWNIWPBUCZWPCUCZUHZWMXKWPBCGUIUJWMXHYEXKRWMXHJZYEBWPDN
      ZOZXKYFYCYHYDYFYHYCBBDNZOZWKYJWLXHADUKZWKYJEABDULUMUNYCYGYIWPBBDUOUPUQYFY
      HYDBCDNZOZWMXHYMWLWKXHYMRZWLWKJZXHYLJOZYNYKYOYPEACBDURUMXHYLUSUTVAVBYDYGY
      LWPCBDUOUPUQVCXHXKYHUEWMXHXJYGXHXIBWPDXHBCVDVEUPVFVGWMXHOZJZYECWPDNZOZXKY
      RYCYTYDYRYTYCYQWMYQVHYCYSXHWPBCDUOUPUQYRYTYDCCDNZOZWLUUBWKYQYKWLUUBEACDUL
      UMVIYDYSUUAWPCCDUOUPUQVCYQXKYTUEWMYQXJYSYQXICWPDXHBCVJVEUPVFVGVKVLVMZWMXN
      GAWMXNWPAIWMXMXCWMXIWNIXMXCXHBCAAVNXBXMHXIWNXAXIWPDUOVOVPVQVRVMZXFXSFXIAW
      OXIUCZWSXLXEXOUUEWRXKGWNUUEWQXJWOXIWPDVSUPVTUUEXDXNGAUUEWTXMXCWOXIWPDUOWA
      VTWBZVOWCFGHAWNDEWDWEXFFAWFUTXFXSFAXIUUFWGWHUUCUUDWIWJ $.
      $( [17-Jun-2007] $)

    $( The supremum of a singleton. $)
    supsn $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( wcel csn csup wbr cif cpr dfsn2 supeq1i wceq suppr anidms syl5eq ifid
      syl6eq ) BAEZBFZACGZBBCHZBBIZBSUABBJZACGZUCATUDCBKLSUEUCMABBCDNOPUBBQR $.
      $( [2-Oct-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.
    supsn.1 $e |- R Or A $.
    $( TO BE REVISED - delete this? $)
    $( The supremum of a singleton.  This version of ~ supsn is proved
       directly. $)
    supsnALT $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( vx vy vz wcel csn csup cv wbr wn wral wrex wi wa crab cuni df-sup wceq
      wreu wb wmo elsni wor sonr mpan breq2 notbid syl5ibr syl com12 r19.21aiv
      snidg rcla4ev sylan ex a1d jca breq1 ralbidv imbi1d anbi12d mpdan supmo
      jctir reu5 sylibr reuuni2 mpbi2and syl5eq ) BAHZBIZACJEKZFKZCLZMZFVNNZVPV
      OCLZVPGKZCLZGVNOZPZFANZQZEARSZBEFGAVNCTVMBVPCLZMZFVNNZVPBCLZWCPZFANZWGBUA
      ZVMWFEAUBZWJWMQZWNUCVMWFEAOZVOAHWFQEUDZQWOVMWQWRVMWPWQVMWJWMVMWIFVNVPVNHZ
      VMWIWSVPBUAZVMWIPVPBUEVMWIWTBBCLZMZACUFVMXBDABCUGUHWTWHXAVPBBCUIUJUKULUMU
      NZVMWLFAVMWLVPAHVMWKWCVMBVNHWKWCBAUOWBWKGBVNWABVPCUIUPUQURUSUNZUTWFWPEBAV
      OBUAZVSWJWEWMXEVRWIFVNXEVQWHVOBVPCVAUJVBXEWDWLFAXEVTWKWCVOBVPCUIVCVBVDZUP
      VEEFGAVNCDVFVGWFEAVHVIWFWPEABXFVJVEXCXDVKVL $.
      $( [23-May-1999] $)
  $}

