$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Add new constant symbol. $)
  $c sup $.  $( Supremum. $)

  $( Extend class notation to include supremum of class ` A ` .  Here ` R ` is
     ordinarily a relation that strictly orders class ` B ` .  For example,
     ` R ` could be 'less than' and ` B ` could be the set of real numbers. $)
  csup $a class sup ( A , B , R ) $.

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Define the supremum of class ` B ` .  It is meaningful when ` R ` is a
       relation that strictly orders ` A ` and when the supremum exists.  For
       example, ` R ` could be 'less than', ` A ` could be the set of real
       numbers, and ` B ` could be the set of all positive reals whose square
       is less than 2; in this case the supremum is defined as the square root
       of 2 per ~ sqrval .

       We will also use this notation for "infimum" by replacing ` R ` with
       ` ``' R ` . $)
    df-sup $a |- sup ( B , A , R ) = U. { x e. A | ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) } $.

    $( can't be used if A is a proper class... important?  we already
       have U. as sup for On... but since sup exists, can't use sup for
       On anyway e.g. sup of On itself.
    df-supNEW $a |- sup = { <. <. x , y >. , z >. | z =
             U. { w e. ( 1st ` x ) | ( A. v e. y -. w ( 2nd ` x ) v /\
                  A. v e. ( 1st ` x ) ( v ( 2nd ` x ) w ->
                 E. u e. y v ( 2nd ` x ) u ) ) } } $.
    $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Equality theorem for supremum. $)
    supeq1 $p |- ( B = C -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup raleq rexeq
      imbi2d ralbidv anbi12d rabbidv unieqd df-sup 3eqtr4g ) BCHZEIZFIZDJKZFBLZ
      UJUIDJZUJGIDJZGBMZNZFALZOZEAPZQUKFCLZUMUNGCMZNZFALZOZEAPZQBADRCADRUHUSVEU
      HURVDEAUHULUTUQVCUKFBCSUHUPVBFAUHUOVAUMUNGBCTUAUBUCUDUEEFGABDUFEFGACDUFUG
      $.
      $( [22-May-1999] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    supmo.1 $e |- R Or A $.
    $( Any class ` B ` has at most one supremum in ` A ` (where ` R ` is
       interpreted as 'less than'). $)
    supmo $p |- E* x ( x e. A /\ ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( vw cv wcel wbr wn wral wrex wi wa wmo wceq wal eleq1 breq1 notbid
      ralbidv breq2 imbi1d anbi12d mo4 wb wor sotrieq2 mpan ad2ant2r rexbidv
      imbi12d rcla4va con3d imp cbvralv ralnex bitri sylan2b an1rs adantlrr
      adantrl rcla4cva anasss adantrrr adantll mpbir2and ax-gen mpgbir ) AIZDJZ
      VLBIZFKZLZBEMZVNVLFKZVNCIZFKZCENZOZBDMZPZPZAQWEHIZDJZWFVNFKZLZBEMZVNWFFKZ
      WAOZBDMZPZPZPZVLWFRZOZHSAWEWOAHWQVMWGWDWNVLWFDTWQVQWJWCWMWQVPWIBEWQVOWHVL
      WFVNFUAUBUCWQWBWLBDWQVRWKWAVLWFVNFUDUEUCUFUFUGWRHWPWQVLWFFKZLZWFVLFKZLZVM
      WGWQWTXBPUHZWDWNDFUIVMWGPXCGDVLWFFUJUKULWEWNWTWGWEWMWTWJVMVQWMWTWCVMWMVQW
      TVMWMPZVLVSFKZCENZLZWTVQXDXGWTXDWSXFWLWSXFOBVLDVNVLRZWKWSWAXFVNVLWFFUAXHV
      TXECEVNVLVSFUAUMUNUOUPUQVQXELZCEMXGVPXIBCEVNVSRZVOXEVNVSVLFUDUBURXECEUSUT
      VAVBVCVDVDWDWOXBVMWCWOXBVQWCWGWJXBWMWCWGWJXBWCWGPZWFVSFKZCENZLZXBWJXKXNXB
      XKXAXMWBXAXMOBWFDVNWFRZVRXAWAXMVNWFVLFUAXOVTXLCEVNWFVSFUAUMUNVEUPUQWJXLLZ
      CEMXNWIXPBCEXJWHXLVNVSWFFUDUBURXLCEUSUTVAVFVGVHVHVIVJVK $.
      $( [5-May-1999] $)

    $( A supremum is a set. $)
    supex $p |- sup ( B , A , R ) e. _V $=
      ( vx vy vz csup cv wcel wbr wn wral wrex wi wa cab cuni cvv crab df-sup
      df-rab unieqi eqtri wmo supmo moabex ax-mp uniex eqeltri ) BACHZEIZAJULFI
      ZCKLFBMUMULCKUMGICKGBNOFAMPZPZEQZRZSUKUNEATZRUQEFGABCUAURUPUNEAUBUCUDUPUO
      EUEUPSJEFGABCDUFUOEUGUHUIUJ $.
      $( [22-May-1999] $)

    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeu $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                  E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( cv wbr wn wral wrex wi wa wcel wmo wreu supmo jctr reu5 sylibr ) AHZBHZ
      FIJBEKUCUBFIUCCHFICELMBDKNZADLZUEUBDOUDNAPZNUDADQUEUFABCDEFGRSUDADTUA $.
      $( [12-Oct-2004] $)

    $( A supremum belongs to its base class (closure law). $)
    supcl $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                sup ( B , A , R ) e. A ) $=
      ( cv wbr wn wral wrex wi wa crab cuni csup wreu wcel supeu reucl syl
      df-sup syl5eqel ) AHZBHZFIJBEKUFUEFIUFCHFICELMBDKNZADLZUGADOPZDEDFQUHUGAD
      RUIDSABCDEFGTUGADUAUBABCDEFUCUD $.
      $( [12-Oct-2004] $)

    $( A supremum is an upper bound. $)
    supub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                 ( C e. B -> -. sup ( B , A , R ) R C ) ) $=
      ( vw wcel cv wbr wn wral wrex wi wa csup wceq breq2 notbid imbi2d rcla4v
      crab cuni df-sup eqcomi wreu wb breq1 ralbidv imbi1d anbi12d reuuni2
      supcl supeu sylanc mpbiri pm3.26d syl5 vtoclga com12 ) FEJAKZBKZGLZMZBENZ
      VDVCGLZVDCKGLCEOZPZBDNZQZADOZEDGRZFGLZMZVMVNIKZGLZMZPVMVPPIFEVQFSZVSVPVMV
      TVRVOVQFVNGTUAUBVQEJVNVDGLZMZBENZVSVMWBVSBVQEVDVQSWAVRVDVQVNGTUAUCVMWCVDV
      NGLZVIPZBDNZVMWCWFQZVLADUDUEZVNSZVNWHABCDEGUFUGVNDJVLADUHWGWIUIVMVLWGADVN
      VCVNSZVGWCVKWFWJVFWBBEWJVEWAVCVNVDGUJUAUKWJVJWEBDWJVHWDVIVCVNVDGTULUKUMUN
      ABCDEGHUOABCDEGHUPUQURUSUTVAVB $.
      $( [12-Oct-2004] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                      E. z e. B C R z ) ) $=
        ( vw cv wbr wn wral wrex wi wa wcel csup wceq breq1 rexbidv imbi12d
        imbi2d rcla4v crab cuni df-sup eqcomi wreu wb notbid ralbidv breq2
        imbi1d anbi12d reuuni2 supcl supeu sylanc mpbiri pm3.27d syl5 vtoclga
        com12 imp3a ) AJZBJZGKZLZBEMZVGVFGKZVGCJZGKZCENZOZBDMZPZADNZFDQZFEDGRZG
        KZFVLGKZCENZVSVRWAWCOZVRIJZVTGKZWEVLGKZCENZOZOVRWDOIFDWEFSZWIWDVRWJWFWA
        WHWCWEFVTGTWJWGWBCEWEFVLGTUAUBUCWEDQVGVTGKZVNOZBDMZWIVRWLWIBWEDVGWESZWK
        WFVNWHVGWEVTGTWNVMWGCEVGWEVLGTUAUBUDVRVTVGGKZLZBEMZWMVRWQWMPZVQADUEUFZV
        TSZVTWSABCDEGUGUHVTDQVQADUIWRWTUJVRVQWRADVTVFVTSZVJWQVPWMXAVIWPBEXAVHWO
        VFVTVGGTUKULXAVOWLBDXAVKWKVNVFVTVGGUMUNULUOUPABCDEGHUQABCDEGHURUSUTVAVB
        VCVDVE $.
        $( [13-Oct-2004] $)

      $( An upper bound is not less than the supremum. $)
      supnub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ A. z e. B -. C R z ) ->
                      -. C R sup ( B , A , R ) ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub expdimp dfrex2 syl6ib
        con2d expimpd ) AIZBIZGJKBELUEUDGJUECIZGJCEMNBDLOADMZFDPZFUFGJZKCELZFED
        GQGJZKUGUHOZUKUJULUKUICEMZUJKUGUHUKUMABCDEFGHRSUICETUAUBUC $.
        $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    sup.1 $e |- R Or A $.
    sup.2 $e |- E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $.
    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeui $p |- E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $=
      ( cv wbr wn wral wrex wi wa wreu supeu ax-mp ) AIZBIZFJKBELTSFJTCIFJCEMNB
      DLOZADMUAADPHABCDEFGQR $.
      $( [23-May-1999] $)

    $( A supremum belongs to its base class (closure law). $)
    supcli $p |- sup ( B , A , R ) e. A $=
      ( cv wbr wn wral wrex wi wa csup wcel supcl ax-mp ) AIZBIZFJKBELUATFJUACI
      FJCEMNBDLOADMEDFPDQHABCDEFGRS $.
      $( [23-May-1999] $)

    $( A supremum is an upper bound. $)
    supubi $p |- ( C e. B -> -. sup ( B , A , R ) R C ) $=
      ( cv wbr wn wral wrex wi wa wcel csup supub ax-mp ) AJZBJZGKLBEMUBUAGKUBC
      JGKCENOBDMPADNFEQEDGRFGKLOIABCDEFGHST $.
      $( [23-May-1999] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplubi $p |- ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                    E. z e. B C R z ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFEDGRGKPFUCGKCENOIABCDEFGHST $.
        $( [23-May-1999] $)

      $( An upper bound is not less than the supremum. $)
      supnubi $p |- ( ( C e. A /\ A. z e. B -. C R z ) ->
                   -. C R sup ( B , A , R ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup supnub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFUCGKLCEMPFEDGRGKLOIABCDEFGHST $.
        $( [23-May-1999] $)
    $}
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( A set that contains a greatest element satisfies the antecedent in
       supremum theorems.  This allows ` sup ( A , B , R ) ` to be used in some
       situations without the completeness axiom.  (Contributed by Jeff
       Hoffman, 17-Jun-2008.) $)
    supmaxlem $p |- ( ( C e. A /\ C e. B /\ A. z e. B -. C R z ) -> E. x e. A
         ( A. y e. B -. x R y /\ A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( wcel cv wbr wn wral wrex wi wa wceq breq1 notbid ralbidv breq2 imbi1d
      anbi12d rcla4ev cbvralv biimpi ex a1d r19.21aiv anim12i ancoms sylan2
      3impb ) FDHZFEHZFCIZGJZKZCELZAIZBIZGJZKZBELZUTUSGJZUTUOGJZCEMZNZBDLZOZADM
      ZUMFUTGJZKZBELZUTFGJZVFNZBDLZOZVJUNUROVIVQAFDUSFPZVCVMVHVPVRVBVLBEVRVAVKU
      SFUTGQRSVRVGVOBDVRVDVNVFUSFUTGTUASUBUCURUNVQURVMUNVPURVMUQVLCBEUOUTPUPVKU
      OUTFGTRUDUEUNVOBDUNVOUTDHUNVNVFVEVNCFEUOFUTGTUCUFUGUHUIUJUKUL $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    supmax.1 $e |- R Or A $.
    $( The greatest element of a set is its supremum.  Note that the converse
       is not true; the supremum might not be an element of the set
       considered.  (Contributed by Jeff Hoffman, 17-Jun-2008.) $)
    supmax $p |- ( ( C e. A /\ C e. B /\ A. y e. B -. C R y )
                           -> sup ( B , A , R ) = C ) $=
      ( vx vz wcel cv wbr wn wral w3a csup wceq wa wb wor sotrieq2 mpan wrex
      wi supmaxlem supcl syl 3simp1 sylanc supub 3simp2 sylc supnub 3simpb
      mpbir2and ) DBIZDCIZDAJZEKLACMZNZCBEOZDPZUTDEKLZDUTEKLZUTBIZUOVAVBVCQRZUS
      BESVDUOQVEFBUTDETUAUSGJZHJZEKLHCMVGVFEKVGUQEKACUBUCHBMQGBUBZVDGHABCDEUDZG
      HABCEFUEUFUOUPURUGUHVHUPVBUSGHABCDEFUIVIUOUPURUJUKVHUOURQVCUSGHABCDEFULVI
      UOUPURUMUKUN $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    suppr.1 $e |- R Or A $.
    $( The supremum of a pair. $)
    suppr $p |- ( ( B e. A /\ C e. A ) -> sup ( { B , C } , A , R ) =
                if ( C R B , B , C ) ) $=
      ( vx vy vz wcel wa cv wbr wn cpr wral wrex wi crab cuni cif csup wceq
      wreu wb breq1 notbid ralbidv breq2 imbi1d anbi12d reuuni2 ifcl wmo
      rcla4ev wo wor sonr mpan ad2antrr syl5cbir so2nr imnan sylibr ancoms imp
      jaod iftrue breq1d adantl sylibrd pm3.27 ad2antlr iffalse pm2.61dan
      visset elpr syl5ib r19.21aiv ifpr sylan ex a1d jca sylanc supmo jctir
      reu5 mpbi2and df-sup syl5eq ) BAIZCAIZJZFKZGKZDLZMZGBCNZOZWOWNDLZWOHKZDLZ
      HWRPZQZGAOZJZFARSZCBDLZBCTZWRADUAWMXIWODLZMZGWROZWOXIDLZXCQZGAOZXGXIUBZXI
      AIZXFFAUCZXLXOJZXPUDWMXFXSFAXIWNXIUBZWSXLXEXOXTWQXKGWRXTWPXJWNXIWODUEUFUG
      XTXDXNGAXTWTXMXCWNXIWODUHUIUGUJZUKXHBCAULZWMXFFAPZWNAIXFJFUMZJXRWMYCYDXQX
      SYCWMXFXSFXIAYAUNYBWMXLXOWMXKGWRWMWOBUBZWOCUBZUOZXKWOWRIWMXHYGXKQWMXHJZYG
      BWODLZMZXKYHYEYJYFYEYJBBDLZMZYHYEYIYKWOBBDUHUFWKYLWLXHADUPZWKYLEABDUQURUS
      UTYFYJBCDLZMZYHYFYIYNWOCBDUHUFWMXHYOWLWKXHYOQZWLWKJZXHYNJMZYPYMYQYREACBDV
      AURXHYNVBVCVDVEUTVFXHXKYJUDWMXHXJYIXHXIBWODXHBCVGVHUFVIVJWMXHMZJZYGCWODLZ
      MZXKYTYEUUBYFYEUUBYSYTYEUUAXHWOBCDUHUFWMYSVKUTYFUUBCCDLZMZYTYFUUAUUCWOCCD
      UHUFWLUUDWKYSYMWLUUDEACDUQURVLUTVFYSXKUUBUDWMYSXJUUAYSXICWODXHBCVMVHUFVIV
      JVNWOBCGVOVPVQVRZWMXNGAWMXNWOAIWMXMXCXIWRIXMXCWMXBXMHXIWRXAXIWODUHUNXHBCA
      AVSVTWAWBVRZWCWDFGHAWRDEWEWFXFFAWGVCWDUUEUUFWHFGHAWRDWIWJ $.
      $( [17-Jun-2007] $)

    $( The supremum of a singleton. $)
    supsn $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( wcel csn csup wbr cif cpr wceq suppr anidms dfsn2 supeq1 ax-mp syl5eq
      ifid syl6eq ) BAEZBFZACGZBBCHZBBIZBTBBJZACGZUDUBTUFUDKABBCDLMUAUEKUBUFKBN
      AUAUECOPQUCBRS $.
      $( [2-Oct-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.
    supsn.1 $e |- R Or A $.
    $( TO BE REVISED - delete this? $)
    $( The supremum of a singleton.  This version of ~ supsn is proved
       directly. $)
    supsnALT $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( vx vy vz wcel cv wbr wn csn wral wrex wi wa crab cuni csup wceq wreu
      wb wmo elsni breq2 notbid wor sonr mpan syl5bir syl com12 r19.21aiv
      rcla4ev snidg sylan ex a1d jca breq1 ralbidv imbi1d anbi12d mpdan supmo
      jctir reu5 sylibr reuuni2 mpbi2and df-sup syl5eq ) BAHZEIZFIZCJZKZFBLZMZV
      OVNCJZVOGIZCJZGVRNZOZFAMZPZEAQRZBVRACSVMBVOCJZKZFVRMZVOBCJZWCOZFAMZWGBTZV
      MWFEAUAZWJWMPZWNUBVMWFEANZVNAHWFPEUCZPWOVMWQWRVMWPWQVMWJWMVMWIFVRVOVRHZVM
      WIWSVOBTZVMWIOVOBUDWTWIBBCJZKZVMWTWHXAVOBBCUEUFACUGVMXBDABCUHUIUJUKULUMZV
      MWLFAVMWLVOAHVMWKWCBVRHWKWCVMWBWKGBVRWABVOCUEUNBAUOUPUQURUMZUSWFWPEBAVNBT
      ZVSWJWEWMXEVQWIFVRXEVPWHVNBVOCUTUFVAXEWDWLFAXEVTWKWCVNBVOCUEVBVAVCZUNVDEF
      GAVRCDVEVFWFEAVGVHWFWPEABXFVIVDXCXDVJEFGAVRCVKVL $.
      $( [23-May-1999] $)
  $}

