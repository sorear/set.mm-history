$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Introduce the Axiom of Union
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z $.
    $( Axiom of Union.  An axiom of Zermelo-Fraenkel set theory.  It states
       that a set ` y ` exists that includes the union of a given set ` x `
       i.e. the collection of all members of the members of ` x ` .  The
       variant ~ axun2 states that the union itself exists.  A version with the
       standard abbreviation for union is ~ uniex2 .  A version using class
       notation is ~ uniex .

       The union of a class ~ df-uni should not be confused with the union of
       two classes ~ df-un .  Their relationship is shown in ~ unipr . $)
    ax-un $a |- E. y A. z ( E. w ( z e. w /\ w e. x ) -> z e. y ) $.

    $( Axiom of Union expressed with fewest number of different variables. $)
    zfun $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wcel wa wex wi wal ax-un wceq elequ2 elequ1 anbi12d cbvexv
      imbi1i albii exbii mpbi ) BEZDEZFZUBCEZFZGZDHZUAAEZFZIZBJZAHUIUHUDFZGZAHZ
      UIIZBJZAHCABDKUKUPAUJUOBUGUNUIUFUMDAUBUHLUCUIUEULDABMDACNOPQRST $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Union ~ ax-un .  For any set ` x ` , there
       exists a set ` y ` whose members are exactly the members of the members
       of ` x ` i.e. the union of ` x ` .  Axiom Union of [BellMachover]
       p. 466. $)
    axun2 $p |- E. y A. z ( z e. y <-> E. w ( z e. w /\ w e. x ) ) $=
      ( cv wcel wa wex ax-un bm1.3ii ) CEDEZFKAEFGDHBCABCDIJ $.
      $( [4-Jun-2006] $)

    $( The Axiom of Union using the standard abbreviation for union.  Given any
       set ` x ` , its union ` y ` exists. $)
    uniex2 $p |- E. y y = U. x $=
      ( vz cv cuni wceq wex wcel wb wal wi wa zfun eluni imbi1i albii exbii
      mpbir bm1.3ii dfcleq ) BDZADZEZFZBGCDZUAHZUEUCHZICJZBGUGBCUGUFKZCJZBGUFUA
      UBHLBGZUFKZCJZBGBCAMUJUMBUIULCUGUKUFBUEUBNOPQRSUDUHBCUAUCTQR $.
      $( [4-Jun-2006] $)
  $}

  ${
    $d x y A $.
    uniex.1 $e |- A e. _V $.
    $( The Axiom of Union in class notation.  This says that if ` A ` is a set
       i.e. ` A e. _V ` (see ~ isset ), then the union of ` A ` is also a set.
       Same as Axiom 3 of [TakeutiZaring] p. 16. $)
    uniex $p |- U. A e. _V $=
      ( vx vy cv cuni cvv wcel wceq unieq eleq1d uniex2 issetri vtocl ) CEZFZGH
      AFZGHCABOAIPQGOAJKDPCDLMN $.
      $( [11-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( The ZF Axiom of Union in class notation, in the form of a theorem
       instead of an inference.  We use the antecedent ` A e. B ` instead of
       ` A e. _V ` to make the theorem more general and thus shorten some
       proofs; obviously ` _V ` is one possibility for ` B ` . $)
    uniexg $p |- ( A e. B -> U. A e. _V ) $=
      ( vx cv cuni cvv wcel wceq unieq eleq1d visset uniex vtoclg ) CDZEZFGAEZF
      GCABNAHOPFNAIJNCKLM $.
      $( [25-Nov-1994] $)
  $}

  ${
    unex.1 $e |- A e. _V $.
    unex.2 $e |- B e. _V $.
    $( The union of two sets is a set.  Corollary 5.8 of [TakeutiZaring]
       p. 16. $)
    unex $p |- ( A u. B ) e. _V $=
      ( cpr cuni cun cvv unipr prex uniex eqeltrri ) ABEZFABGHABCDIMABJKL $.
      $( [1-Jul-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Existence of union is equivalent to existence of its components. $)
    unexb $p |- ( ( A e. _V /\ B e. _V ) <-> ( A u. B ) e. _V ) $=
      ( vx vy cvv wcel wa cun cv wceq uneq1 eleq1d uneq2 visset unex vtocl2g
      wss ssun1 ssexg mpan ssun2 jca impbii ) AEFZBEFZGABHZEFZCIZDIZHZEFAUIHZEF
      UGCDABEEUHAJUJUKEUHAUIKLUIBJUKUFEUIBAMLUHUICNDNOPUGUDUEAUFQUGUDABRAUFESTB
      UFQUGUEBAUABUFESTUBUC $.
      $( [11-Jun-1998] $)
  $}

  $( A union of two sets is a set.  Corollary 5.8 of [TakeutiZaring] p. 16. $)
  unexg $p |- ( ( A e. C /\ B e. D ) -> ( A u. B ) e. _V ) $=
    ( cvv wcel cun wa unexb biimpi elisset syl2an ) AEFZBEFZABGEFZACFBDFMNHOABI
    JACKBDKL $.
    $( [18-Sep-2006] $)

  ${
    $( A version of ~ unisn without the ` A e. _V ` hypothesis.  (Contributed
       by Stefan Allan, 14-Mar-2006.) $)
    unisn2 $p |- U. { A } e. { (/) , A } $=
      ( cvv wcel csn cuni c0 cpr unisng wceq wo eqid olci elprg mpbiri eqeltrd
      wn snprc biimpi unieqd uni0 0ex prid1 eqeltri syl6eqel pm2.61i ) ABCZADZE
      ZFAGZCUFUHAUIABHUFAUICAFIZAAIZJUKUJAKLAFABMNOUFPZUHFEZUIULUGFULUGFIAQRSUM
      FUITFAUAUBUCUDUE $.
      $( [14-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Union of a singleton in the form of a restricted class abstraction. $)
    unisn3 $p |- ( A e. B -> U. { x e. B | x = A } = A ) $=
      ( wcel cv wceq crab cuni csn rabsn unieqd unisng eqtrd ) BCDZAEBFACGZHBIZ
      HBNOPACBJKBCLM $.
      $( [3-Jul-2008] $)
  $}

  ${
    $d x y z $.
    $( The class of all singletons is a proper class.  (Proof shortened by Eric
       Schmidt, 7-Dec-2008.) $)
    snnex $p |- { x | E. y x = { y } } e/ _V $=
      ( vz cv csn wceq wex cab cvv wnel wcel wn cuni vprc wa visset snid
      isseti sneq eqcoms eximi ax-mp snex eleq2 eqeq1 exbidv anbi12d cla4ev
      mp2an eluniab mpbir 2th eqriv eleq1i mtbir uniexg mto df-nel ) ADZBDZEZFZ
      BGZAHZIJVDIKZLVEVDMZIKZVGIIKNVFIICVFICDZVFKZVHIKVIVHUSKZVCOZAGZVHVHEZKZVM
      VAFZBGZVLVHCPZQUTVHFZBGVPBVHVQRVRVOBVOVHUTVHUTSTUAUBVKVNVPOAVMVHUCUSVMFZV
      JVNVCVPUSVMVHUDVSVBVOBUSVMVAUEUFUGUHUIVCAVHUJUKVQULUMUNUOVDIUPUQVDIURUK
      $.
      $( [10-Oct-2008] $)
  $}

  $( If the subtrahend of a class difference exists, then the minuend exists
     iff the difference exists.  (The proof was shortened by Andrew Salmon,
     12-Aug-2011.) $)
  difex2 $p |- ( B e. C -> ( A e. _V <-> ( A \ B ) e. _V ) ) $=
    ( wcel cvv cdif difexg cun wss wa ssexg ssun2 uncom undif2 eqtr2i sseqtri
    unexg sylancr expcom impbid2 ) BCDZAEDZABFZEDZABEGUDUAUBAUCBHZIUEEDUBUDUAJA
    UEEKABAHZUEABLUEBUCHUFUCBMBANOPUCBECQRST $.
    $( [12-Aug-2011] $) $( [12-Nov-2003] $)

  $( A triple of classes exists. $)
  tpex $p |- { A , B , C } e. _V $=
    ( ctp cpr csn cun cvv df-tp prex snex unex eqeltri ) ABCDABEZCFZGHABCINOABJ
    CKLM $.
    $( [10-Apr-1994] $)

  $( Each member of an ordered pair belongs to the union of the union of a
     class to which the ordered pair belongs.  Lemma 3D of [Enderton] p. 41. $)
  opeluu $p |- ( <. x , y >. e. A ->
               ( x e. U. U. A /\ y e. U. U. A ) ) $=
    ( cv cop wcel cuni cpr elunii visset prid1 opi2 mpan sylancr prid2 jca ) AD
    ZBDZEZCFZQCGZGZFZRUBFZQQRHZFUEUAFZUCTQUEUAIQRAJKUESFTUFQRLUESCIMZNRUEFUFUDT
    RUEUAIQRBJOUGNP $.
    $( [31-Mar-1995] $)

  ${
    $d A x y v z $.  $d A x y u z $.
    $( Expression for double union that moves union into a class builder.
       (Contributed by FL, 28-May-2007.) $)
    uniuni $p |- U. U. A = U. { x | E. y ( x = U. y /\ y e. A ) } $=
      ( vz vu vv cv wcel cuni wa wex cab wceq eluni anbi2i exbii 19.42v bicomi
      excom anass ancom bitr3i 2exbii bitri exdistr 3bitri visset uniex eleq2
      ceqsexv exancom 3bitr2i eqeq1 anbi1d exbidv elab abbii df-uni 3eqtr4i )
      DGZEGZHZVACIZHZJZEKZDLUTFGZHZVGAGZBGZIZMZVJCHZJZBKZALZHZJZFKZDLVCIVPIVFVS
      DVFVBVAVJHZVMJZBKZJZEKZVMUTVKHZJZBKZVSVEWCEVDWBVBBVACNOPWDVMVBVTJZEKZJZBK
      ZWGWDVBWAJZBKZEKZVMWHJZEKBKZWKWCWMEWMWCVBWABQRPWNWLEKBKWPWLEBSWLWOBEWLWHV
      MJWOVBVTVMTWHVMUAUBUCUDVMWHBEUEUFWJWFBWIWEVMWEWIEUTVJNROPUDWGVHVGVKMZVMJZ
      JZBKFKZVHWRBKZJZFKVSWGWSFKZBKWTWFXCBWFVMVHWQJZFKZJVMXDJZFKXCWEXEVMWEWQVHJ
      FKXEVHWEFVKVJBUGUHVGVKUTUIUJWQVHFUKUBOVMXDFQXFWSFXFXDVMJWSVMXDUAVHWQVMTUD
      PULPWSBFSUDVHWRFBUEXBVRFXAVQVHVQXAVOXAAVGFUGVIVGMZVNWRBXGVLWQVMVIVGVKUMUN
      UOUPROPUFUFUQDEVCURDFVPURUS $.
      $( [5-Feb-2008] $) $( [28-May-2007] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( If ` ph ` is true for exactly one ` x ` , then ` U. { x | ph } ` is a
       way to express "the unique element such that ` ph ` is true."  Some
       books use a special symbol such as iota to denote "the unique element
       such that." $)
    euuni $p |- ( E! x ph -> ( ph <-> U. { x | ph } = x ) ) $=
      ( vy vz weu cab cuni cv wceq wa wex wb cvv wcel euabex uniexg syl eueq
      eqcom eubii hbab1 hbuni ax-17 hbeq eqeq2 cbveu 3bitri sylib csn euabsn
      visset snid eleq2 mpbiri abid unieq unisn syl6eq jca eximi sylbi eupickb
      mpd3an23 ) ABEZABFZGZBHZIZBEZAVHJZBKZAVHLVDVFMNZVIVDVEMNVLABOVEMPQVLCHZVF
      IZCEVFVMIZCEVICVFRVNVOCVMVFSTVOVHCBBDDVFVMBDVEABDUAUBDHVMNBUCUDVHCUCVMVGV
      FUEUFUGUHVDVEVGUIZIZBKVKABUJVQVJBVQAVHVQVGVENZAVQVRVGVPNVGBUKZULVEVPVGUMU
      NABUOUHVQVFVPGVGVEVPUPVGVSUQURUSUTVAAVHBVBVC $.
      $( [22-Feb-2004] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( A way to express "the unique element such that" (restricted quantifier
       version).  (The proof was shortened by Andrew Salmon, 12-Aug-2011.) $)
    reuuni1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
            ( ph <-> U. { x e. A | ph } = x ) ) $=
      ( wreu cv wcel crab cuni wceq wb wa cab ibar df-rab unieqi eqeq1i a1i
      bibi12d weu df-reu euuni sylbi syl5cbir impcom ) ABCDZBEZCFZAABCGZHZUFIZJ
      ZUGUKUGAKZULBLZHZUFIZJZUEUGAULUJUOUGAMUJUOJUGUIUNUFUHUMABCNOPQRUEULBSUPAB
      CTULBUAUBUCUD $.
      $( [12-Aug-2011] $) $( [25-Nov-1994] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    reuuni2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    reuuni2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    reuuni2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ."  This theorem shows a condition
       that allows us to represent this element with a class expression
       ` B ` .  The second hypothesis is a weakened bound variable condition
       that allows ~ hbsbc1g to be used. $)
    reuuni2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( wcel wreu crab cuni wceq wb wi cv ax-17 hbel wal hbreu1 a1i hbrab1
      hbuni hbeq hbbid hbimd hbim1 eleq1 eqeq2 bibi12d imbi2d imbi12d reuuni1
      ex vtoclgf pm2.43i imp ) FEJZACEKZBACELZMZFNZOZUSUTVDPZCQZEJZUTAVBVFNZOZP
      ZPUSVEPCDFEGUSVECCDDFEGDQEJCRSZUSUTVDCVKUTUTCTPUSACEUAUBUSBVCCVKHVCVCCTPU
      SCDDVBFCDVAACDEUCUDGUEUBUFUGUHVFFNZVGUSVJVEVFFEUIVLVIVDUTVLABVHVCIVFFVBUJ
      UKULUMVGUTVIACEUNUOUPUQUR $.
      $( [19-Oct-2005] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reuuni2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ." $)
    reuuni2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( vy cv wcel ax-17 wal wi a1i reuuni2f ) ABCGDEGHEICJBBCKLEDIBCJMFN $.
      $( [27-May-2006] $) $( [28-Mar-1997] $)
  $}

  ${
    $d A x $.  $d ps x $.
    euuni2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( The unique element such that ` ph ` .  (Contributed by Jeff Madsen,
       1-Jun-2011.) $)
    euuni2 $p |- ( ( A e. B /\ E! x ph ) -> ( ps <-> U. { x | ph } = A ) ) $=
      ( cvv wcel wreu cab cuni wceq wb weu wa crab reuuni2 rabab unieqi eqeq1i
      syl6bb elisset reuv biimpri syl2an ) DGHZACGIZBACJZKZDLZMDEHACNZUFUGOBACG
      PZKZDLUJABCGDFQUMUIDULUHACRSTUADEUBUGUKACUCUDUE $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.  $d x ch $.
    reuuni3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    reuuni3.2 $e |- ( x = U. { y e. A | ps } -> ( ph <-> ch ) ) $.
    $( Derive the property ` ch ` of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { y e. A | ps } ` . $)
    reuuni3 $p |- ( E! x e. A ph -> ch ) $=
      ( crab cuni wcel wreu reucl cbvrabv unieqi syl5eqelr wa wceq reuuni2
      mpbiri mpancom ) BEFIZJZFKZADFLZCUEADFIZJZFUCADFMUFUBABDEFGNOZPUDUEQCUGUC
      RUHACDFUCHSTUA $.
      $( [11-Nov-2004] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Derive the property of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { x e. A | ph } ` . $)
    reuuni4 $p |- ( E! x e. A ph -> [ U. { x e. A | ph } / x ] ph ) $=
      ( vy wreu crab cuni wcel wsbc reucl cvv wrex wi reurex hbreu1 hbrab1
      hbuni hbsbc1 cv wa wceq wb reuuni1 sbceq1a eqcoms syl6bi ibd expcom a1i
      com4l r19.23ad mpd elisset syl5 ) ABCEZABCFZGZCHZABUQIZABCJUOUQKHZUSURUOA
      BCLUTUSMZABCNUOAVABCABCOABDUQKBDUPABDCPQRUTUOBSZCHZAUSUOVCAUSMZMMUTVCUOVD
      VCUOTZAUSVEAUQVBUAAUSUBZABCUCVFVBUQABUQUDUEUFUGUHUIUJUKULUQCUMUNUL $.
      $( [11-Nov-2004] $)

    $( Membership law for "the unique element in ` A ` such that ` ph ` ." $)
    reucl2 $p |- ( E! x e. A ph -> U. { x e. A | ph } e. { x e. A | ph } ) $=
      ( vy crab cuni wcel wsbc wreu cv ax-17 elrabsf reucl reuuni4 sylanbrc )
      ABCEZFZPGQCGABQHABCIABDQCDJCGBKLABCMABCNO $.
      $( [11-Jun-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss $p |- ( ( A C_ B /\ E. x e. A ph /\ E! x e. B ph ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( vy wss wrex wreu w3a crab cuni wsbc wceq reuss reuuni4 syl wcel wb
      reucl wi ssel 3ad2ant1 mpd simp3 hbrab1 hbuni hbsbc1g sbceq1a reuuni2f
      syl11anc mpbid eqcomd ) CDFZABCGZABDHZIZABDJKZABCJZKZUPABUSLZUQUSMZUPABCH
      ZUTABCDNZABCOPUPUSDQZUOUTVARUPUSCQZVDUPVBVEVCABCSPUMUNVEVDTUOCDUSUAUBUCUM
      UNUOUDAUTBEDUSBEURABECUEUFZABEUSDVFUGABUSUHUIUJUKUL $.
      $( [19-Oct-2005] $)

    $( Restriction of a unique element to a smaller class. $)
    mouniss $p |- ( ( A C_ B /\ E. x e. A ph /\ E* x ( x e. B /\ ph ) ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( wss wrex cv wcel wa wmo crab cuni wceq wreu ssel anim1d reximdv2 imp
      anim1i reu5 sylibr reuuniss 3expa syldan 3impa ) CDEZABCFZBGZDHZAIBJZABCK
      LABDKLMZUFUGIZUJABDNZUKULUJIABDFZUJIUMULUNUJUFUGUNUFAABCDUFUHCHUIACDUHOPQ
      RSABDTUAUFUGUMUKABCDUBUCUDUE $.
      $( [19-Feb-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) ) -> U. { x e. A | ph } = U. { x e. B | ps } ) $=
      ( vy wss wi wral wa wrex wreu crab cuni wsbc wceq reuss2 simplr reuuni4
      wcel reucl ra4sbc sbcimg sylibd syl mpid sylc wb ssel ad2antrr mpd
      simprr hbrab1 hbuni hbsbc1g sbceq1a reuuni2f syl11anc mpbid eqcomd ) DEGZ
      ABHZCDIZJZACDKZBCELZJZJZBCEMNZACDMZNZVHBCVKOZVIVKPZVHACDLZVCVLABCDEQZVAVC
      VGRVNVCACVKOZVLACDSVNVKDTZVCVPVLHZHACDUAZVQVCVBCVKOVRVBCVKDUBABCVKDUCUDUE
      UFUGVHVKETZVFVLVMUHVHVQVTVHVNVQVOVSUEVAVQVTHVCVGDEVKUIUJUKVDVEVFULBVLCFEV
      KCFVJACFDUMUNZBCFVKEWAUOBCVKUPUQURUSUT $.
      $( [20-Oct-2005] $)
  $}

  ${
    $d y z A $.  $d y z ph $.  $d x y z $.
    $( A way to express restricted existential uniqueness of a wff: its
       restricted class abstraction is a singleton.  (The proof was shortened
       by Andrew Salmon, 12-Aug-2011.) $)
    reusn $p |- ( E! x e. A ph <-> E. y { x e. A | ph } = { y } ) $=
      ( vz wreu cv wcel wa weu cab csn wceq wex crab df-reu euabsn ax-17
      hbrab1 hbeq df-rab eqcomi a1i sneq eqeq12d cbvex 3bitri ) ABDFBGZDHAIZBJU
      IBKZUHLZMZBNABDOZCGZLZMZCNABDPUIBQULUPBCULCRBEEUMUOABEDSEGUOHBRTUHUNMZUJU
      MUKUOUJUMMUQUMUJABDUAUBUCUHUNUDUEUFUG $.
      $( [12-Aug-2011] $) $( [30-May-2006] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y ph $.  $d x y $.
    reusni.1 $e |- B e. _V $.
    $( Restricted existential uniqueness determined by a singleton. $)
    reusni $p |- ( { x e. A | ph } = { B } -> E! x e. A ph ) $=
      ( vy crab csn wceq cv wex wreu sneq eqeq2d cla4ev reusn sylibr ) ABCGZDHZ
      IZRFJZHZIZFKABCLUCTFDEUADIUBSRUADMNOABFCPQ $.
      $( [29-May-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rabsnt.1 $e |- B e. _V $.
    rabsnt.2 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Truth implied by equality of a restricted class abstraction and a
       singleton. $)
    rabsnt $p |- ( { x e. A | ph } = { B } -> ps ) $=
      ( crab csn wceq wcel wreu wa cuni wss ssrab2 sseq1 mpbii snss sylibr
      reusni jca unieq unisn syl6eq reuuni2 biimprd sylc ) ACDHZEIZJZEDKZACDLZM
      ZUINZEJZBUKULUMUKUJDOZULUKUIDOUQACDPUIUJDQREDFSTACDEFUAUBUKUOUJNEUIUJUCEF
      UDUEUNBUPABCDEGUFUGUH $.
      $( [29-May-2006] $)
  $}

  ${
    $d y A $.  $d y ph $.  $d x y $.
    $( A restricted class abstraction with a unique member can be expressed as
       a singleton. $)
    reuunisn $p |- ( E! x e. A ph ->
                   { x e. A | ph } = { U. { x e. A | ph } } ) $=
      ( vy wreu crab cv csn wceq wex cuni reusn unieq visset unisn syl6eq
      sneqd eqtr3 mpdan 19.23aiv sylbi ) ABCEABCFZDGZHZIZDJUBUBKZHZIZABDCLUEUHD
      UEUGUDIUHUEUFUCUEUFUDKUCUBUDMUCDNOPQUBUGUDRSTUA $.
      $( [30-May-2006] $)
  $}

  ${
    $d x y z $.  $d A y z $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    eualeq $p |- ( E! y A. x y = A <-> E. y A. x y = A ) $=
      ( vz cv wceq wal weu wex wa wi eqeq1 albidv eu4 eqtr3 ax-4 syl2an gen2
      mpbiran2 ) BEZCFZAGZBHUBBIUBDEZCFZAGZJTUCFZKZDGBGUBUEBDUFUAUDATUCCLMNUGBD
      UAUDUFUBUETUCCOUAAPUDAPQRS $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y z w $.  $d A y z w $.
    $( Even if ` x ` is free in ` A ` , it is effectively bound when
       ` A ( x ) ` is single-valued. $)
    eualeqhb $p |- ( E! y A. x y = A -> ( y = A -> A. x y = A ) ) $=
      ( vz vw cv wceq wal weu cvv wreu wi wcel wa visset biantrur eubii df-reu
      bitr4i crab cuni eqeq1 albidv ax-17 hba1 hbrab hbuni hbeq albid reuuni3
      19.21bi syl5cbir eqtr3 syl5 mpan2d sylbi ) BFZCGZAHZBIZUSBJKZURUSLUTUQJMZ
      USNZBIVAUSVCBVBUSBOPQUSBJRSVAURDFZCGZAHZDJTZUAZCGZUSVAVIAUSVFVIAHZBDJUQVD
      GURVEAUQVDCUBUCUQVHGZURVIAAEEUQVHEFZUQMAUDAEVGVFADEJVEAUEVLJMAUDUFUGUHUQV
      HCUBUIZUJZUKVAVKUSURVINVKUSVJVAVMVNULUQVHCUMUNUOUP $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y $.  $d A y $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    eualexeq $p |- ( E! y A. x y = A -> E! y E. x y = A ) $=
      ( cv wceq wal weu wex hbeu1 hba1 hbeu eualeqhb 19.23ad 19.2 impbid1
      eubid ibir ) BDCEZAFZBGZRAHZBGTUASBSBITUASTRSASABRAJZKUBABCLMRAANOPQ $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y z v w $.  $d A y z w v $.
    euexeqOLD.1 $e |- A e. _V $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    euexeqOLD $p |- ( E! y E. x y = A <-> E! y A. x y = A ) $=
      ( vz vw vv cv wceq wex weu wal cvv crab cuni wcel wreu wa visset
      biantrur eubii ax-17 eqeq1 exbidv cbveu df-reu 3bitr4i reucl sylbi hbe1
      hbeu eqid wi eu4 hbrab hbuni hbeq exbid anbi1d imbi12d albidv cla4gv
      com12 adantl mpd bitr4i reuuni3 biantrurd imbi1d mpbird 19.23 albii
      alcom bitr3i sylib ax-4 eqeq2 cla4v 3syl mpi 19.21ai albid cla4egv sylc
      eqtr3 syl2an gen2 mpbiran2 bicomi hbeu1 hba1 19.21bi syl5cbir syl5
      mpan2d 19.23ad 19.2 impbid1 eubid ibir impbii ) BHZCIZAJZBKZXCALZBKZXEXFB
      JZXGXEEHZCIZAJZEMNZOZMPZXMCIZALZXHXEXKEMQZXNXKEKXIMPZXKRZEKXEXQXKXSEXRXKE
      STUAXDXKBEXDEUBXKBUBXBXIIZXCXJAXBXICUCZUDZUEXKEMUFUGXKEMUHUIZXEXOAXDABXCA
      UJUKXECCIZXOCULXEFHZCIZXMYEIZUMZFLZALZYIYDXOUMZXEYFAJZYGUMZFLZYJXEYNXOAJZ
      YLRZYGUMZFLZXEXNYRYCXEXDBJZXDYLRZXBYEIZUMZFLZBLZRXNYRUMZXDYLBFUUAXCYFAXBY
      ECUCUDUNUUDUUEYSXNUUDYRUUCYRBXMMXBXMIZUUBYQFUUFYTYPUUAYGUUFXDYOYLUUFXCXOA
      AGGXBXMGHZXBPAUBZAGXLXKAEGMXJAUJUUGMPAUBZUOUPZUQZXBXMCUCZURZUSXBXMYEUCUTV
      AVBVCVDUIVEXEYMYQFXEYLYPYGXEYOYLXEXDBMQZYOXEXBMPZXDRZBKUUNXDUUPBUUOXDBSZT
      UAXDBMUFVFXDXKYOBEMYBUUMVGUIVHVIVAVJYNYHALZFLYJUURYMFYFYGAAGGXMYEUUJUUGYE
      PAUBUQVKVLYHFAVMVNVOYIAVPYHYKFCDYFYFYDYGXOYECCUCYECXMVQUTVRVSVTWAXFXPBXMM
      UUFXCXOAUUKUULWBWCWDXGXHXGXHXFXJALZRXTUMZELBLXFUUSBEXTXCXJAYAVAZUNUUTBEXC
      XJXTXFUUSXBXICWEXCAVPXJAVPWFWGWHWIVOXGXEXGXDXFBXFBWJXGXDXFXGXCXFAXFABXCAW
      KZUKUVBXGXFBMQZXCXFUMXGUUOXFRZBKUVCXFUVDBUUOXFUUQTUAXFBMUFVFUVCXCUUSEMNZO
      ZCIZXFUVCUVGAXFUUSUVGALZBEMUVAXBUVFIZXCUVGAAGGXBUVFUUHAGUVEUUSAEGMXJAWKUU
      IUOUPUQXBUVFCUCWBZVGZWLUVCUVIXFXCUVGRUVIXFUVHUVCUVJUVKWMXBUVFCWEWNWOUIWPX
      CAAWQWRWSWTXA $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y z v w $.  $d A y z w v $.
    euexaleq.1 $e |- A e. _V $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    euexaleq $p |- ( E! y E. x y = A <-> E! y A. x y = A ) $=
      ( vz vw vv cv wceq wex weu wal cvv crab cuni wcel wreu wa visset
      biantrur eubii ax-17 eqeq1 exbidv cbveu df-reu 3bitr4i reucl sylbi hbe1
      hbeu eqid wi eu4 hbrab hbuni hbeq exbid anbi1d imbi12d albidv cla4gv
      com12 adantl mpd bitr4i reuuni3 biantrurd imbi1d mpbird 19.23 albii
      alcom bitr3i sylib ax-4 eqeq2 cla4v 3syl mpi 19.21ai albid cla4egv sylc
      eualeq sylibr eualexeq impbii ) BHZCIZAJZBKZWJALZBKZWLWMBJZWNWLEHZCIZAJZE
      MNZOZMPZWTCIZALZWOWLWREMQZXAWREKWPMPZWRRZEKWLXDWRXFEXEWRESTUAWKWRBEWKEUBW
      RBUBWIWPIWJWQAWIWPCUCUDZUEWREMUFUGWREMUHUIZWLXBAWKABWJAUJUKWLCCIZXBCULWLF
      HZCIZWTXJIZUMZFLZALZXNXIXBUMZWLXKAJZXLUMZFLZXOWLXSXBAJZXQRZXLUMZFLZWLXAYC
      XHWLWKBJZWKXQRZWIXJIZUMZFLZBLZRXAYCUMZWKXQBFYFWJXKAWIXJCUCUDUNYIYJYDXAYIY
      CYHYCBWTMWIWTIZYGYBFYKYEYAYFXLYKWKXTXQYKWJXBAAGGWIWTGHZWIPAUBAGWSWRAEGMWQ
      AUJYLMPAUBUOUPZUQZWIWTCUCZURZUSWIWTXJUCUTVAVBVCVDUIVEWLXRYBFWLXQYAXLWLXTX
      QWLWKBMQZXTWLWIMPZWKRZBKYQWKYSBYRWKBSTUAWKBMUFVFWKWRXTBEMXGYPVGUIVHVIVAVJ
      XSXMALZFLXOYTXRFXKXLAAGGWTXJYMYLXJPAUBUQVKVLXMFAVMVNVOXNAVPXMXPFCDXKXKXIX
      LXBXJCCUCXJCWTVQUTVRVSVTWAWMXCBWTMYKWJXBAYNYOWBWCWDABCWEWFABCWGWH $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y z B $.  $d x z C $.  $d x y D $.  $d x z ph $.  $d x y ps $.
    eufromeq0.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    eufromeq0.2 $e |- ( y = z -> C = D ) $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq0i $p |- ( E. x e. A A. y e. B ( ph -> x = C )
            -> A. y e. B A. z e. B ( ( ph /\ ps ) -> C = D ) ) $=
      ( cv wceq wi wral wa wcel eqeq2d imbi12d cbvralv biimpi raaanv prth
      eqtr2 syl6 ralimi sylbir mpdan a1i r19.23aiv ) ACLZHMZNZDGOZABPZHIMZNZEGO
      ZDGOZCFUNUSNUKFQUNBUKIMZNZEGOZUSUNVBUMVADEGDLELMZABULUTJVCHIUKKRSTUAUNVBP
      UMVAPZEGOZDGOUSUMVADEGUBVEURDGVDUQEGVDUOULUTPUPAULBUTUCUKHIUDUEUFUFUGUHUI
      UJ $.
      $( [23-Dec-1012] $)

    $d x y z A $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       See ~ eufromeq6 for the connection to uniqueness. $)
    eufromeq0a $p |- ( E. y e. B ( ph /\ C e. A )
          -> ( A. y e. B A. z e. B ( ( ph /\ ps ) -> C = D )
                   <-> E. x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( wcel wa wrex wceq wi wral cv eleq1d anbi12d cbvrexv ax-17 hbra1 hbral
      hbim eqeq1 eqcom syl6bb imbi2d ralbidv ralcom impexp bi2.04 bitri ralbii
      r19.21v ra4 sylbi com3l imp31 syl5cbir reximdv ex com23 risset syl5ib
      expimpd r19.23ai eufromeq0i impbid1 ) AHFLZMZDGNZABMHIOZPZEGQZDGQZACRZHOZ
      PZDGQZCFNZVMBIFLZMZEGNVQWBPZVLWDDEGDRZERZOZABVKWCJWHHIFKSTUAWDWEEGVQWBEVP
      EDGWFGLEUBVOEGUCUDWBEUBUEWGGLZBWCWEWIBMZVRIOZCFNZWEWCWJVQWLWBWJVQWLWBPWJV
      QMZWKWACFWKWAAVNPZDGQZWMWKVTWNDGWKVSVNAWKVSIHOVNVRIHUFIHUGUHUIUJWIBVQWOVQ
      WIBWOVQBWOPZEGQZWIWPPVQVODGQZEGQWQVODEGGUKWRWPEGWRBWNPZDGQWPVOWSDGVOABVNP
      PWSABVNULABVNUMUNUOBWNDGUPUNUOUNWPEGUQURUSUTVAVBVCVDCIFVEVFVGVHURABCDEFGH
      IJKVIVJ $.
      $( [27-Dec-1012] $)

    eufromeq0.3 $e |- ( ( y e. B /\ ph ) -> C e. A ) $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       See ~ eufromeq6 for the connection to uniqueness. $)
    eufromeq0 $p |- ( E. y e. B ph
          -> ( A. y e. B A. z e. B ( ( ph /\ ps ) -> C = D )
                   <-> E. x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( wrex wa wceq wi wral cv wcel cbvrexv eleq1d imbi12d ex vtoclga ancld
      reximia sylbi ax-17 hbra1 hbral hbim eqeq1 eqcom syl6bb imbi2d ralbidv
      ralcom impexp bi2.04 bitri ralbii r19.21v ra4 com3l imp31 syl5cbir
      reximdv com23 risset syl5ib expimpd r19.23ai syl eufromeq0i impbid1 ) AD
      GMZABNHIOZPZEGQZDGQZACRZHOZPZDGQZCFMZVPBIFSZNZEGMZVTWEPZVPBEGMWHABDEGJTBW
      GEGERZGSZBWFAHFSZPBWFPDWJGDRZWJOZABWLWFJWNHIFKUAUBWMGSZAWLLUCUDUEUFUGWGWI
      EGVTWEEVSEDGWOEUHVREGUIUJWEEUHUKWKBWFWIWKBNZWAIOZCFMZWIWFWPVTWRWEWPVTWRWE
      PWPVTNZWQWDCFWQWDAVQPZDGQZWSWQWCWTDGWQWBVQAWQWBIHOVQWAIHULIHUMUNUOUPWKBVT
      XAVTWKBXAVTBXAPZEGQZWKXBPVTVRDGQZEGQXCVRDEGGUQXDXBEGXDBWTPZDGQXBVRXEDGVRA
      BVQPPXEABVQURABVQUSUTVABWTDGVBUTVAUTXBEGVCUGVDVEVFVGUCVHCIFVIVJVKVLVMABCD
      EFGHIJKVNVO $.
      $( [23-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq1 $p |- ( A =/= (/) -> ( E! x A. y e. A x = B
                     <-> E. x A. y e. A x = B ) ) $=
      ( vz c0 wne cv wcel wex wceq wral weu wb n0 hbra1 hbeu hbex hbbi wa wi
      wal ra4 eqtr3 syl6com r19.26 syl5ibr 19.21aivv biantrud eqeq1 ralbidv
      eu4 syl6rbbr 19.23ai sylbi ) CFGBHCIZBJAHZDKZBCLZAMZUSAJZNZBCOUPVBBUTVABU
      SBAURBCPZQUSBAVCRSUPVAVAUSEHZDKZBCLZTZUQVDKZUAZEUBAUBZTUTUPVJVAUPVIAEUPUR
      VETZBCLZVHVGVLUPVKVHVKBCUCUQVDDUDUEURVEBCUFUGUHUIUSVFAEVHURVEBCUQVDDUJUKU
      LUMUNUO $.
      $( [22-Oct-2010] $)
  $}

  ${
    $d v w x y z A $.  $d v w x z B $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq2 $p |- ( E! x A. y e. A x = B -> E! x E. y e. A x = B ) $=
      ( vz vw vv cv wceq wral weu wrex wi c0 wn wal wex eunex exnal sylib rzal
      19.21aiv nsyl3 pm2.21 syl wne wa eqeq1 rexbidv eu4 euex adantl r19.2z ex
      eximdv adantr mpd hbeu1 csb cvv crab cuni wreu ralbidv wcel ax-17 hbra1
      hbrab hbuni hbeq ralbid reuuni3 visset biantrur eubii df-reu bitr4i
      anidm 3imtr4i hbcsb1 raaan csbeq1a eqeq2d cbvral anbi2i eqeq12 eqtr2
      syl5cbir ralimi sylbir r19.23v ralbii bitri hbeleq reean cbvrex syl5ibr
      19.21ai sylanbrc pm2.61ine ) AHZDIZBCJZAKZXBBCLZAKZMZCNCNIZXDOXGXDXCAPZXH
      XDXCOAQXIOXCARXCASTXHXCAXBBCUAUBUCXDXFUDUECNUFZXDXFXFXEAQZXEEHZDIZBCLZUGZ
      XAXLIZMZEPZAPZXJXDUGZXEXNAEXPXBXMBCXAXLDUHZUIUJXTXCAQZXKXDYBXJXCAUKULXJYB
      XKMXDXJXCXEAXJXCXEXBBCUMUNUOUPUQXDXSXJXDXRAXCAURXDXQEXDXBXLBFHZDUSZIZUGZF
      CLZBCLZXPXOXDYFXPMZFCJZBCJZYHXPMZXDXMBCJZEUTVAZVBZDIZBCJZYQUGZYKXCAUTVCZY
      QXDYRXCYMYQAEUTXPXBXMBCYAVDXAYOIXBYPBCBGGXAYOGHZXAVEBVFBGYNYMBEGUTXMBCVGY
      TUTVEBVFVHVIZVJXAYODUHVKVLXDXAUTVEZXCUGZAKYSXCUUCAUUBXCAVMVNVOXCAUTVPVQYQ
      VRVSYRYPYOYDIZUGZFCJZBCJZYKUUGYQUUDFCJZUGYRYPUUDBFCYPFVFZBGGYOYDUUABGYCDF
      VMZYTYCVEBVFVTVJZWAYQUUHYQYPUUDBFCUUIUUKBHYCIZDYDYOBYCDWBZWCWDWEVQUUFYJBC
      UUEYIFCYFXPDYDIUUEXADXLYDWFYODYDWGWHWIWIWJUEYKYGXPMZBCJYLYJUUNBCYFXPFCWKW
      LYGXPBCWKWMTYHXEYEFCLZUGXOXBYEBFCCXBFVFBEYDBEYCDUUJXLYCVEBVFVTWNZWOXNUUOX
      EXMYEBFCXMFVFUUPUULDYDXLUUMWCWPWEVQWQUBWRULWSUNWT $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq3g $p |- ( A. y e. A B e. _V ->
          ( E! x E. y e. A x = B <-> E! x A. y e. A x = B ) ) $=
      ( vz cvv wcel wral cv wceq wrex weu wi c0 wex rex0 rexeq mtbiri nexdv
      euex nsyl pm2.21d a1i wne wa weq wal eqeq1 imbi12d eqid a1bi eqcom
      bitr3i syl6bb cla4gv ral2imi ralcom4 r19.23v albii bitri syl5ibr eximdv
      ralbidv cbvexv syl6ibr adantld ax-17 eu3 syl5ib adantr wb eufromeq1
      adantl sylibrd ex pm2.61dne eufromeq2 impbid1 ) DFGZBCHZAIZDJZBCKZALZWBBC
      HZALZVTWDWFMZCNCNJZWGMVTWHWDWFWHWCAOZWDWHWCAWHWCWBBNKWBBPWBBCNQRSWCATUAUB
      UCVTCNUDZWGVTWJUEWDWEAOZWFVTWDWKMWJVTWIWCAEUFZMZAUGZEOZUEWKWDVTWOWKWIVTWO
      EIZDJZBCHZEOWKVTWNWREVTWBWLMZAUGZBCHZWRWNVSWTWQBCWSWQADFWBWSDDJZDWPJZMZWQ
      WBWBXBWLXCWADDUHWADWPUHUIXDXCWQXBXCDUJUKDWPULUMUNUOUPXAWSBCHZAUGWNWSBACUQ
      XEWMAWBWLBCURUSUTVAVBWEWRAEWLWBWQBCWAWPDUHVCVDVEVFWCAEWCEVGVHVIVJWJWFWKVK
      VTABCDVLVMVNVOVPABCDVQVR $.
      $( [14-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    eufromeq3.1 $e |- B e. _V $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq3 $p |- ( E! x E. y e. A x = B <-> E! x A. y e. A x = B ) $=
      ( cvv wcel cv wceq wrex weu wral wb eufromeq3g a1i mprg ) DFGZAHDIZBCJAKR
      BCLAKMBCABCDNQBHCGEOP $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d x z ph $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       This shows ~ eufromeq3 in a more general form. $)
    eufromeq4 $p |- ( E! x e. A E. y e. B ( ph /\ x = C )
            <-> E! x A. y e. B ( ( C e. A /\ ph ) -> x = C ) ) $=
      ( vz cv wceq wa wrex wreu wcel weu csb cab wi wral df-reu r19.42v wex
      df-rex abid anbi1i eleq1 anbi2d anbi1d pm5.32ri anass bitr3i 3bitrri
      exbii ax-17 hbab1 visset hbcsb1 hbeleq hban csbeq1a eqeq2d anbi12d cbvex
      bitr4i 3bitri eubii cvv wb eufromeq3g simplr ss2abi sseli hbel eleq1d
      elabf elisset sylbi syl mprg wal df-ral hbim imbi12d cbval bitri imbi1i
      impexp albii 3bitr2i ) ABHZFIZJZCEKZBDLWIDMZWLJZBNWICGHZFOZIZGCHZEMZFDMZJ
      ZAJZCPZKZBNZWTAJZWJQZCERZBNZWLBDSWNXDBWNWMWKJZCEKZXDWMWKCETXKWSXJJZCUAWRX
      CMZWJJZCUAZXDXJCEUBXLXNCXNXBWJJZWSWMJZWKJZXLXMXBWJXBCUCZUDXPXQAJZWJJXRWJX
      TXBWJXQXAAWJWMWTWSWIFDUEUFUGUHXQAWJUIUJWSWMWKUIUKULXOWOXCMZWQJZGUAXDXNYBC
      GXNGUMYAWQCXBCGUNZCBWPCBWOFGUOZWIWOMCUMUPZUQZURWRWOIZXMYAWJWQWRWOXCUEZYGF
      WPWICWOFUSZUTZVAVBWQGXCUBVCVDUJVEXEWQGXCRZBNZXIWPVFMZXEYLVGGXCBGXCWPVHYAW
      OWTCPZMZYMXCYNWOXBWTCWSWTAVIVJVKYOWPDMZYMWTYPCWOCBGWPDYEWODMCUMVLYDYGFWPD
      YIVMVNWPDVOVPVQVRYKXHBYKYAWQQZGVSXMWJQZCVSZXHWQGXCVTYRYQCGYRGUMYAWQCYCYFW
      AYGXMYAWJWQYHYJWBWCYSWSXGQZCVSXHYRYTCYRWSXFJZWJQYTXMUUAWJXMXBUUAXSWSWTAUI
      WDWEWSXFWJWFWDWGXGCEVTVCWHVEWDVD $.
      $( [13-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.
    $( Lemma for ~ eufromeq6 and ~ eufromeq5 . $)
    eufromeq5lem $p |- ( B =/= (/) -> ( E! x e. A A. y e. B x = C
                     <-> E. x e. A A. y e. B x = C ) ) $=
      ( vz c0 wne cv wcel wex wceq wral wreu wrex wb n0 ax-17 hbra1 hbreu
      hbrex hbbi wa wi ra4 eqtr3 syl6com r19.26 syl5ibr a1d r19.21aivv
      biantrud eqeq1 ralbidv reu4 syl6rbbr 19.23ai sylbi ) DGHBIDJZBKAIZELZBDMZ
      ACNZVBACOZPZBDQUSVEBVCVDBVBBACUTCJZBRZVABDSZTVBBACVGVHUAUBUSVDVDVBFIZELZB
      DMZUCZUTVILZUDZFCMACMZUCVCUSVOVDUSVNAFCCUSVNVFVICJUCUSVAVJUCZBDMZVMVLVQUS
      VPVMVPBDUEUTVIEUFUGVAVJBDUHUIUJUKULVBVKAFCVMVAVJBDUTVIEUMUNUOUPUQUR $.
      $( [22-Oct-2010] $)
  $}

  ${
    $d x z A $.  $d w x z B $.  $d x z C $.  $d w x z ph $.  $d w x y z $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq6 $p |- ( E. y e. B ph
       -> ( E! x e. A A. y e. B ( ph -> x = C )
                   <-> E. x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw wrex cv wcel wa cab c0 wne wceq wi wral wreu wb crab rabn0
      df-rab neeq1i bitr3i csb eufromeq5lem hbab1 ax-17 visset hbcsb1 hbeleq
      csbeq1a eqeq2d cbvralf reubii rexbii 3bitr4g abid imbi1i impexp bitr2i
      ralbii2 sylbi ) ACEIZCJZEKZALZCMZNOZABJZFPZQZCERZBDSZVNBDIZTVEACEUAZNOVJA
      CEUBVQVINACEUCUDUEVJVLCVIRZBDSZVRBDIZVOVPVJVKCGJZFUFZPZGVIRZBDSWDBDIVSVTB
      GDVIWBUGVRWDBDVLWCCGHVIVHCHUHHJVIKGUIVLGUICBWBCBWAFGUJVKWAKCUIUKULVFWAPFW
      BVKCWAFUMUNUOZUPVRWDBDWEUQURVNVRBDVMVLCEVIVFVIKZVLQVHVLQVGVMQWFVHVLVHCUSU
      TVGAVLVAVBVCZUPVNVRBDWGUQURVD $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x C $.  $d x y $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq5 $p |- ( B =/= (/) -> ( E! x e. A A. y e. B x = C
                     <-> E. x e. A A. y e. B x = C ) ) $=
      ( c0 wne cv wceq wrex wral wreu wb wcel wex wa equid biantru exbii n0
      df-rex 3bitr4i wi eufromeq6 a1bi ralbii reubii rexbii 3bitr4g sylbi )
      DFGZBHZULIZBDJZAHEIZBDKZACLZUPACJZMULDNZBOUSUMPZBOUKUNUSUTBUMUSBQZRSBDTUM
      BDUAUBUNUMUOUCZBDKZACLVCACJUQURUMABCDEUDUPVCACUOVBBDUMUOVAUEUFZUGUPVCACVD
      UHUIUJ $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w x z C $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       The converse does not hold.  Note that ` U. A = |^| A ` means ` A ` is a
       singleton ( ~ uniintsn ). $)
    eufromeq7 $p |- ( ( U. A =/= |^| A \/ B =/= (/) )
          -> ( E! x e. A A. y e. B x = C -> E! x e. A E. y e. B x = C ) ) $=
      ( vz vw vv cuni cint wne cv wceq wral wreu wrex wi c0 wn raleq reubidv
      wcel wa weu df-reu csn wex uniintsn eusn ral0 biantru eubii 3bitr2i
      bitr4i syl6bb necon3bbid pm2.21 syl6bir eqeq1 rexbidv reu4 reurex adantl
      r19.2z ex reximdv adantr mpd hbreu1 csb crab ralbidv ax-17 hbra1 hbrab
      hbuni hbeq ralbid reuuni3 anidm sylibr visset hbcsb1 raaan csbeq1a
      eqeq2d cbvral anbi2i eqeq12 eqtr2 syl5cbir ralimi sylbir syl r19.23v
      ralbii bitri sylib hbeleq reean cbvrex syl5ibr a1d r19.21aiv r19.21ai
      sylanbrc pm2.61ine jaoi ) CIZCJZKZALZEMZBDNZACOZXMBDPZACOZQZDRKZXKXRQDRDR
      MZXKXOSXRXTXOXIXJXTXOXMBRNZACOZXIXJMZXTXNYAACXMBDRTUAYBXLCUBZYAUCZAUDZYCY
      AACUEYCCXLUFMAUGYDAUDYFACUHACUIYDYEAYAYDXMBUJUKULUMUNUOUPXOXQUQURXSXRXKXS
      XOXQXQXPACPZXPFLZEMZBDPZUCZXLYHMZQZFCNZACNZXSXOUCZXPYJAFCYLXMYIBDXLYHEUSZ
      UTVAYPXNACPZYGXOYRXSXNACVBVCXSYRYGQXOXSXNXPACXSXNXPXMBDVDVEVFVGVHXOYOXSXO
      YNACXNACVIXOYNYDXOYMFCXOYMYHCUBXOXMYHBGLZEVJZMZUCZGDPZBDPZYLYKXOUUBYLQZGD
      NZBDNZUUDYLQZXOYIBDNZFCVKZIZEMZBDNZUUMUCZUUGXOUUMUUNXNUUIUUMAFCYLXMYIBDYQ
      VLXLUUKMXMUULBDBHHXLUUKHLZXLUBBVMBHUUJUUIBFHCYIBDVNUUOCUBBVMVOVPZVQXLUUKE
      USVRVSUUMVTWAUUNUULUUKYTMZUCZGDNZBDNZUUGUUTUUMUUQGDNZUCUUNUULUUQBGDUULGVM
      ZBHHUUKYTUUPBHYSEGWBZUUOYSUBBVMWCVQZWDUUMUVAUUMUULUUQBGDUVBUVDBLYSMZEYTUU
      KBYSEWEZWFWGWHUNUUSUUFBDUURUUEGDUUBYLEYTMUURXLEYHYTWIUUKEYTWJWKWLWLWMWNUU
      GUUCYLQZBDNUUHUUFUVGBDUUBYLGDWOWPUUCYLBDWOWQWRUUDXPUUAGDPZUCYKXMUUABGDDXM
      GVMBFYTBFYSEUVCYHYSUBBVMWCWSZWTYJUVHXPYIUUABGDYIGVMUVIUVEEYTYHUVFWFXAWHUN
      XBXCXDXCXEVCXFVEZXCXGUVJXH $.
      $( [30-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.
    eufromeq8.1 $e |- ( y e. B -> C e. A ) $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       Note that ` U. A = |^| A ` means ` A ` is a singleton ( ~ uniintsn ). $)
    eufromeq8 $p |- ( ( U. A =/= |^| A \/ B =/= (/) )
          -> ( E! x e. A E. y e. B x = C <-> E! x e. A A. y e. B x = C ) ) $=
      ( cuni cint wne cv wceq wrex wreu wral wb c0 wi wn wa raleq reubidv wcel
      weu df-reu csn wex uniintsn eusn ral0 biantru eubii 3bitr2i bitr4i
      syl6bb necon3bbid biimprd reurex rexn0 a1i r19.23aiv syl necon2bi jctild
      pm5.21 syl6 r19.28zv eubidv eufromeq4 equid biantrur rexbii reubii
      biantrurd eleq1 pm5.32ri syl6rbbr biimt bitrd imbi1i ralbiia 3bitr4i
      3bitr4g a1d pm2.61ine jaoi ) CGZCHZIZAJZEKZBDLZACMZWJBDNZACMZOZDPIZWHWOQD
      PDPKZWHWLRZWNRZSWOWQWHWSWRWQWSWHWQWNWFWGWQWNWJBPNZACMZWFWGKZWQWMWTACWJBDP
      TUAXAWICUBZWTSZAUCZXBWTACUDXBCWIUEKAUFXCAUCXEACUGACUHXCXDAWTXCWJBUIUJUKUL
      UMUNUOUPWLDPWLWKACLWPWKACUQWKWPACWKWPQXCWJBDURUSUTVAVBVCWLWNVDVEWPWOWHWPX
      CWJSZBDNZAUCZXCWMSZAUCWLWNWPXGXIAXCWJBDVFVGBJZXJKZWJSZBDLZACMECUBZXKSZWJQ
      ZBDNZAUCWLXHXKABCDEVHWKXMACWJXLBDXKWJBVIZVJVKVLXGXQAXFXPBDXJDUBZXFXNWJQZX
      PXSXFWJXTXSWJXNWJSXFXSXNWJFVMWJXCXNWIECVNVOVPXSXNWJXTOFXNWJVQVAVRXNXOWJXK
      XNXRUJVSUNVTUKWAWMACUDWBZWCWDYAWE $.
      $( [14-Dec-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    eufromeq8a $p |- ( ( A. y e. B C e. A /\ B =/= (/) )
          -> ( E! x e. A E. y e. B x = C <-> E! x e. A A. y e. B x = C ) ) $=
      ( wcel wral c0 wne wa cv wceq wrex wreu weu wi wb hbra1 id imp eleq1
      pm5.32ri baib biimt bitrd equid biantru imbi1i syl6rbb syl ra4 sylan
      ralbida eubidv adantr eufromeq4 syl5bb biantrur rexbii reubii r19.28zv
      adantl df-reu syl6bbr ) ECFZBDGZDHIZJZAKZELZBDMZACNZVICFZVJBDGZJZAOZVNACN
      VHVLVMVJJZBDGZAOZVPVHBKZVTLZVJJZBDMZACNZVSVLVHVEWAJZVJPZBDGZAOZVSWDVFWHVS
      QVGVFWGVRAVFWFVQBDVEBDRVTDFZVEPZWIWFVQQZVFWJWIJVEWKWJWIVEWJSTVEVQVEVJPZWF
      VEVQVJWLVQVEVJVJVMVEVIECUAUBUCVEVJUDUEVEWEVJWAVEBUFZUGUHUIUJVEBDUKULUMUNU
      OWAABCDEUPUQVKWCACVJWBBDWAVJWMURUSUTUQVGVSVPQVFVGVRVOAVMVJBDVAUNVBUEVNACV
      CVD $.
      $( [4-Jan-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x z B $.  $d x z C $.  $d w x z ph $.
    eufromeq9.1 $e |- ( ( y e. B /\ ph ) -> C e. A ) $.
    $( Two ways of expressing existential uniqueness via an indirect equality.
       Note that ` U. A = |^| A ` means ` A ` is a singleton ( ~ uniintsn ). $)
    eufromeq9 $p |- ( ( U. A =/= |^| A \/ E. y e. B ph )
          -> ( E! x e. A E. y e. B ( ph /\ x = C )
                   <-> E! x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw cuni cint wne wrex wo cv wcel wa cab c0 wceq wreu wi wral wb
      crab rabn0 df-rab neeq1i bitr3i orbi2i csb wsbc df-clab sbimi sylbi cvv
      visset sbcel1g ax-mp sylib eufromeq8 hbab1 ax-17 hbcsb1 hbeleq csbeq1a
      eqeq2d cbvrexf reubii cbvralf 3bitr4g abid anbi1i anass bitr2i rexbii2
      imbi1i impexp ralbii2 ) DJDKLZACEMZNVTCOZEPZAQZCRZSLZNZABOZFTZQZCEMZBDUAZ
      AWIUBZCEUCZBDUAZUDWAWFVTWAACEUEZSLWFACEUFWPWESACEUGUHUIUJWGWICWEMZBDUAZWI
      CWEUCZBDUAZWLWOWGWHCHOZFUKZTZHWEMZBDUAXCHWEUCZBDUAWRWTBHDWEXBXAWEPZFDPZCX
      AULZXBDPZXFWDCXAULXHWDHCUMWDXGCHGUNUOXAUPPXHXIUDHUQZCXAFDUPURUSUTVAWQXDBD
      WIXCCHIWEWDCIVBZIOWEPHVCZWIHVCZCBXBCBXAFXJWHXAPCVCVDVEZWBXATFXBWHCXAFVFVG
      ZVHVIWSXEBDWIXCCHIWEXKXLXMXNXOVJVIVKWKWQBDWJWICEWEWBWEPZWIQWDWIQWCWJQXPWD
      WIWDCVLZVMWCAWIVNVOVPVIWNWSBDWMWICEWEXPWIUBWDWIUBWCWMUBXPWDWIXQVQWCAWIVRV
      OVSVIVKUO $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d w x y z A $.  $d w x z B $.  $d w x z C $.  $d w x z ph $.
    $( Two ways of expressing existential uniqueness via an indirect equality. $)
    eufromeq9a $p |- ( ( A. y e. B ( ph -> C e. A ) /\ E. y e. B ph )
          -> ( E! x e. A E. y e. B ( ph /\ x = C )
                   <-> E! x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw cv csb wcel wa cab wral c0 wne wceq wrex wreu wi wb eufromeq8a
      hbab1 ax-17 visset wel hbcsb1 hbeleq weq csbeq1a eqeq2d cbvrexf reubii
      cbvralf 3bitr4g abid anbi1i anass bitr2i rexbii2 imbi1i impexp ralbii2
      wal df-ral hbel hbim wsb sbequ12 sbim df-clab cvv sbcel1g ax-mp bicomi
      imbi12i bitr4i syl6bb cbval albii 3bitr2ri bitri crab rabn0 df-rab
      neeq1i bitr3i syl2anb ) CGIZFJZDKZGCIZEKZALZCMZNZWOOPZABIZFQZLZCERZBDSZAW
      STZCENZBDSZUAAFDKZTZCENZACERZWPWQLZWSCWORZBDSZWSCWONZBDSZXBXEXJWRWJQZGWOR
      ZBDSXOGWONZBDSXLXNBGDWOWJUBXKXPBDWSXOCGHWOWNCHUCZHIZWOKGUDZWSGUDZCBWJCBWI
      FGUEZBGUFCUDUGUHZCGUIZFWJWRCWIFUJUKZULUMXMXQBDWSXOCGHWOXRXTYAYCYEUNUMUOXA
      XKBDWTWSCEWOWLWOKZWSLWNWSLWMWTLYFWNWSWNCUPZUQWMAWSURUSUTUMXDXMBDXCWSCEWOY
      FWSTWNWSTWMXCTYFWNWSYGVAWMAWSVBUSVCUMUOXHWMXGTZCVDZWPXGCEVEWPWIWOKZWKTZGV
      DWNXFTZCVDYIWKGWOVEYLYKCGYLGUDYJWKCWNCGUCCHHWJDCHWIFYBHGUFCUDUGXSDKCUDVFV
      GYDYLYLCGVHZYKYLCGVIYMWNCGVHZXFCGVHZTYKWNXFCGVJYJYNWKYOWNGCVKYOWKWIVLKYOW
      KUAYBCWIFDVLVMVNVOVPVQVRVSYLYHCWMAXFVBVTWAWBXIACEWCZOPWQACEWDYPWOOACEWEWF
      WGWH $.
      $( [4-Jan-1013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    euobj1.1 $e |- B e. _V $.
    $( The two existential uniqueness expressions of ~ eufromeq3 specify the
       same object. $)
    euobj1 $p |- ( E! x E. y e. A x = B
       -> U. { x | E. y e. A x = B } = U. { x | A. y e. A x = B } ) $=
      ( vz cv wceq wrex weu wcel wex cab cuni wral wa cvv crab eufromeq3 wreu
      reuv eqeq1 ralbidv hbra1 ax-17 hbrab hbuni hbeleq ralbid reuuni3 sylbir
      sylbi rexbidv hbre1 rexbid r19.29 syl11anc eqtr3 cbvabv rabab eqtr4i
      unieqi 3eqtr4g ancoms reximi syl df-rex hbab hbeq 19.41 bitri sylib
      simprd ) AGZDHZBCIZAJZBGCKZBLZVPAMZNZVOBCOZAMZNZHZVQWEBCIZVSWEPZVQFGZDHZB
      COZFQRZNZDHZWIBCIZFQRZNZDHZPZBCIZWFVQWMBCOZWQBCIZWSVQWBAJZWTABCDESXBWBAQT
      WTWBAUAWBWJWTAFQVNWHHZVOWIBCVNWHDUBZUCZVNWLHVOWMBCBAWLBAWKWJBFAQWIBCUDVNQ
      KBUEZUFUGUHVNWLDUBUIUJUKULVQVPAQTXAVPAUAVPWNXAAFQXCVOWIBCXDUMZVNWPHVOWQBC
      BAWPBAWOWNBFAQWIBCUNXFUFUGUHVNWPDUBUOUJUKWMWQBCUPUQWRWEBCWQWMWEWQWMPWPWLW
      AWDWPWLDURVTWOVTWNFMWOVPWNAFXGUSWNFUTVAVBWCWKWCWJFMWKWBWJAFXEUSWJFUTVAVBV
      CVDVEVFWFVRWEPBLWGWEBCVGVRWEBBFFWAWDBFVTVPBAFVOBCUNVHUGBFWCWBBAFVOBCUDVHU
      GVIVJVKVLVM $.
      $( [1-Nov-2010] $)

    $( The two existential uniqueness expressions of ~ eufromeq3 specify the
       same property. $)
    euobj2 $p |- ( E! x E. y e. A x = B
       -> ( E. y e. A x = B <-> A. y e. A x = B ) ) $=
      ( cv wceq wrex weu cab wcel wral cvv crab cuni csn euobj1 rabab unieqi
      3eqtr4g sneqd wreu reuv reuunisn sylbir eufromeq3 bitr4i sylbi 3eqtr4d
      3eqtr3g eleq2d abid 3bitr3g ) AFZDGZBCHZAIZUNUPAJZKUNUOBCLZAJZKUPUSUQURUT
      UNUQUPAMNZUSAMNZURUTUQVAOZPZVBOZPZVAVBUQVCVEUQUROUTOVCVEABCDEQVAURUPARZSV
      BUTUSARZSTUAUQUPAMUBVAVDGUPAUCUPAMUDUEUQUSAMUBZVBVFGUQUSAIVIABCDEUFUSAUCU
      GUSAMUDUHUIVGVHUJUKUPAULUSAULUM $.
      $( [29-Jul-2011] $) $( [1-Nov-2010] $)
  $}

  ${
    $d x A $.  $d y ph $.  $d x ps $.  $d x y $.
    alxfr.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    alxfr $p |- ( ( A. y A e. B /\ A. x E. y x = A ) ->
                ( A. x ph <-> A. y ps ) ) $=
      ( wcel wal cv wceq wex wa wi hba1 ax-17 cla4gv a4s 19.21ad adantr hbim
      ax-4 syl5bir 19.23ai adantl impbid ) EFHZDIZCJEKZDLZCIZMACIZBDIZUHULUMNUK
      UHULBDUGDOULDPUGULBNDABCEFGQRSTUKUMULNUHUKUMACUJCOUMCPUJUMANZCUIUNDUMADBD
      OADPUAUIABUMGBDUBUCUDRSUEUF $.
      $( [18-Feb-2007] $)
  $}

  ${
    $d x y ph $.  $d x ch $.  $d y ps $.  $d x A $.  $d x y B $.
    ralxfrd.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    ralxfrd.2 $e |- ( ( ph /\ x e. B ) -> E. y e. B x = A ) $.
    ralxfrd.3 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrd $p |- ( ph -> ( A. x e. B ps <-> A. y e. B ch ) ) $=
      ( wral cv wcel wi ex rcla4dv syld com23 r19.21adv wceq wrex wa ax-17
      hbra1 hban ra4 exbiri sylan9r r19.23ad syl5d impbid ) ABDGKZCEGKZAULCEGAE
      LGMZULCAUNFGMZULCNZAUNUOHOAUOUPABCDFGJPOQRSAUMBDGAUMDLZFTZEGUAZBUQGMZAUMU
      SBNAUMUBURBEGAUMEAEUCCEGUDUEBEUCUMUNCAURBNCEGUFAURCBAURBCJUGRUHUIOAUTUSIO
      UJSUK $.
      $( [17-Feb-2007] $)

    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  (Contributed by FL,
       10-Apr-2007.) $)
    rexxfrd $p |- ( ph -> ( E. x e. B ps <-> E. y e. B ch ) ) $=
      ( wn wral wrex cv wceq wa notbid ralxfrd dfrex2 3bitr4g ) ABKZDGLZKCKZEGL
      ZKBDGMCEGMAUBUDAUAUCDEFGHIADNFOPBCJQRQBDGSCEGST $.
      $( [10-Apr-2007] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    ralxfr.1 $e |- ( y e. B -> A e. B ) $.
    ralxfr.2 $e |- ( x e. B -> E. y e. B x = A ) $.
    ralxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfr $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( wral cv wcel wi rcla4v syl com12 r19.21aiv wceq wrex hbra1 ax-17 ra4
      biimprcd syl6 r19.23ad syl5 impbii ) ACFJZBDFJZUHBDFDKFLZUHBUJEFLUHBMGABC
      EFINOPQUIACFUICKZERZDFSAUKFLUIULADFBDFTADUAUIUJBULAMBDFUBULABIUCUDUEHUFQU
      G $.
      $( [10-Jun-2005] $)

    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrALT $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( cvv wceq wral wb eqid cv wcel adantl wrex ralxfrd ax-mp ) JJKZACFLBDFLM
      JNUAABCDEFDOFPEFPUAGQCOZFPUBEKZDFRUAHQUCABMUAIQST $.
      $( [10-Jun-2005] $)

    $( Transfer existence from a variable ` x ` to another variable ` y `
       contained in expression ` A ` . $)
    rexxfr $p |- ( E. x e. B ph <-> E. y e. B ps ) $=
      ( wn wral wrex cv wceq notbid ralxfr notbii dfrex2 3bitr4i ) AJZCFKZJBJZD
      FKZJACFLBDFLUAUCTUBCDEFGHCMENABIOPQACFRBDFRS $.
      $( [10-Jun-2005] $)
  $}

  ${
    $d x A $.  $d z B $.  $d z C $.  $d x y z D $.  $d y ph $.  $d y z ps $.
    $d x z ch $.
    rabxfrd.1 $e |- ( z e. B -> A. y z e. B ) $.
    rabxfrd.2 $e |- ( z e. C -> A. y z e. C ) $.
    rabxfrd.3 $e |- ( ( ph /\ y e. D ) -> A e. D ) $.
    rabxfrd.4 $e |- ( x = A -> ( ps <-> ch ) ) $.
    rabxfrd.5 $e |- ( y = B -> A = C ) $.
    $( Class builder membership after substituting an expression ` A `
       (containing ` y ` ) for ` x ` in the class expression ` ch ` . $)
    rabxfrd $p |- ( ( ph /\ B e. D ) ->
                 ( C e. { x e. D | ps } <-> B e. { y e. D | ch } ) ) $=
      ( wcel crab wb wa wi cv ex ibibr sylib imp anbi1d elrab rabid 3bitr4g
      rabbidva eleq2d ax-17 hbel wceq eleq1d elrabf hbrab1 eleq1 3bitr3g
      pm5.32 sylibr ) AHJPZIBDJQZPZHCEJQZPZRZAVBVDSZVBVFSZRVBVGTAHGVCPZEJQZPHEU
      AZVEPZEJQZPVHVIAVKVNHAVJVMEJAVLJPZSZGJPZCSVOCSVJVMVPVQVOCAVOVQVORZAVOVQTV
      OVRTAVOVQMUBVOVQUCUDUEUFBCDGJNUGCEJUHUIUJUKVJVDEFHJKFUAZJPEULZEFFIVCLVSVC
      PEULUMVLHUNGIVCOUOUPVMVFEFHJKVTEFFHVEKCEFJUQUMVLHVEURUPUSVBVDVFUTVAUE $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x A $.  $d z B $.  $d z C $.  $d x y z D $.  $d y z ph $.  $d x z ps $.
    rabxfr.1 $e |- ( z e. B -> A. y z e. B ) $.
    rabxfr.2 $e |- ( z e. C -> A. y z e. C ) $.
    rabxfr.3 $e |- ( y e. D -> A e. D ) $.
    rabxfr.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    rabxfr.5 $e |- ( y = B -> A = C ) $.
    $( Class builder membership after substituting an expression ` A `
       (containing ` y ` ) for ` x ` in the class expression ` ph ` . $)
    rabxfr $p |- ( B e. D ->
                 ( C e. { x e. D | ph } <-> B e. { y e. D | ps } ) ) $=
      ( cv wceq wcel crab wb equid adantl rabxfrd mpan ) COZUDPZGIQHACIRQGBDIRQ
      SCTUEABCDEFGHIJKDOIQFIQUELUAMNUBUC $.
      $( [10-Jun-2005] $)
  $}

  ${
    $d x y ph $.  $d x ps $.  $d x A $.  $d x y B $.
    reuxfr2d.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    reuxfr2d.2 $e |- ( ( ph /\ x e. B ) -> E* y ( y e. B /\ x = A ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    reuxfr2d $p |- ( ph
        -> ( E! x e. B E. y e. B ( x = A /\ ps ) <-> E! y e. B ps ) ) $=
      ( cv wceq wa wrex wreu wcel wmo wral wi moan syl ancom anass bitri mobii
      sylib r19.21aiva 2reuswap moeq moani an12 mpbi a1i mprg impbid1 wb biidd
      ceqsrexv reubidva bitrd ) ACIZEJZBKZDFLCFMZVACFLZDFMZBDFMAVBVDADIFNZVAKZD
      OZCFPVBVDQAVGCFAUSFNZKZBVEUTKZKZDOZVGVIVJDOVLHVJBDRSVKVFDVKVJBKVFBVJTVEUT
      BUAUBUCUDUEVACDFUFSVHVAKZCOZVDVBQDFVADCFUFVNVEVHBKZUTKZCOVNUTVOCCEUGUHVPV
      MCVPUTVOKVMVOUTTUTVHBUIUBUCUJUKULUMAVCBDFAVEKEFNVCBUNGBBCEFUTBUOUPSUQUR
      $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x y B $.
    reuxfr2.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr2.2 $e |- ( x e. B -> E* y ( y e. B /\ x = A ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    reuxfr2 $p |- ( E! x e. B E. y e. B ( x = A /\ ph ) <-> E! y e. B ph ) $=
      ( cv wceq wa wrex wreu wcel wmo wi 2reuswap moan syl ancom anass bitri
      mobii sylib mprg moeq moani an12 mpbi a1i impbii wb biidd ceqsrexv
      reubiia ) BHZDIZAJZCEKBELZUQBEKZCELZACELURUTCHEMZUQJZCNZURUTOBEUQBCEPUOEM
      ZAVAUPJZJZCNZVCVDVECNVGGVEACQRVFVBCVFVEAJVBAVESVAUPATUAUBUCUDVDUQJZBNZUTU
      ROCEUQCBEPVIVAVDAJZUPJZBNVIUPVJBBDUEUFVKVHBVKUPVJJVHVJUPSUPVDAUGUAUBUHUIU
      DUJUSACEVADEMUSAUKFAABDEUPAULUMRUNUA $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d x y ph $.  $d y ps $.  $d x ch $.  $d x A $.  $d x y B $.
    reuxfrd.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    reuxfrd.2 $e |- ( ( ph /\ x e. B ) -> E! y e. B x = A ) $.
    reuxfrd.3 $e |- ( x = A -> ( ps <-> ch ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  Use ~ reuhypd to
       eliminate the second hypothesis. $)
    reuxfrd $p |- ( ph -> ( E! x e. B ps <-> E! y e. B ch ) ) $=
      ( wreu cv wceq wa wrex wcel reurex syl biantrurd r19.41v pm5.32i rexbii
      bitr3i syl6bb reubidva wmo weu df-reu eumo sylbi reuxfr2d bitrd ) ABDGKDL
      ZFMZCNZEGOZDGKCEGKABUPDGAUMGPNZBUNEGOZBNZUPUQURBUQUNEGKZURIUNEGQRSUSUNBNZ
      EGOUPUNBEGTVAUOEGUNBCJUAUBUCUDUEACDEFGHUQUTELGPUNNZEUFZIUTVBEUGVCUNEGUHVB
      EUIUJRUKUL $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    reuxfr.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr.2 $e |- ( x e. B -> E! y e. B x = A ) $.
    reuxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  Use ~ reuhyp to
       eliminate the second hypothesis. $)
    reuxfr $p |- ( E! x e. B ph <-> E! y e. B ps ) $=
      ( wceq wreu wb eqid cv wcel adantl reuxfrd ax-mp ) FFJZACFKBDFKLFMSABCDEF
      DNFOEFOSGPCNZFOTEJDFKSHPIQR $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d y ph $.  $d y B $.  $d y C $.  $d x y $.
    reuhypd.1 $e |- ( ( ph /\ x e. C ) -> B e. C ) $.
    reuhypd.2 $e |- ( ( ph /\ x e. C /\ y e. C ) -> ( x = A <-> y = B ) ) $.
    $( A theorem useful for eliminating the restricted existential uniqueness
       hypotheses in ~ riotaxfrd . $)
    reuhypd $p |- ( ( ph /\ x e. C ) -> E! y e. C x = A ) $=
      ( cv wcel wa wceq weu wreu cvv elisset syl eueq sylib eleq1 syl5cbir
      pm4.71rd wb 3expa pm5.32da bitr4d eubidv mpbid df-reu sylibr ) ABIZFJZKZC
      IZFJZUKDLZKZCMZUPCFNUMUNELZCMZURUMEOJZUTUMEFJZVAGEFPQCERSUMUSUQCUMUSUOUSK
      UQUMUSUOUSUOVBUMUNEFTGUAUBUMUOUPUSAULUOUPUSUCHUDUEUFUGUHUPCFUIUJ $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d y B $.  $d y C $.  $d x y $.
    reuhyp.1 $e |- ( x e. C -> B e. C ) $.
    reuhyp.2 $e |- ( ( x e. C /\ y e. C ) -> ( x = A <-> y = B ) ) $.
    $( A theorem useful for eliminating the restricted existential uniqueness
       hypotheses in ~ reuxfr . $)
    reuhyp $p |- ( x e. C -> E! y e. C x = A ) $=
      ( cv wceq wcel wreu equid adantl wb 3adant1 reuhypd mpan ) AHZRIZREJZRCIZ
      BEKALSABCDETDEJSFMTBHZEJUAUBDINSGOPQ $.
      $( [15-Nov-2004] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d ph y z $.  $d ps x z $.
    reuunixfr.1 $e |- ( z e. C -> A. y z e. C ) $.
    reuunixfr.2 $e |- ( y e. A -> B e. A ) $.
    reuunixfr.3 $e |- ( U. { y e. A | ps } e. A -> C e. A ) $.
    reuunixfr.4 $e |- ( x = B -> ( ph <-> ps ) ) $.
    reuunixfr.5 $e |- ( y = U. { y e. A | ps } -> B = C ) $.
    reuunixfr.6 $e |- ( x e. A -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` A ` such
       that ` ph ` " to another variable ` y ` contained in expression ` B ` .
       Use ~ reuhyp to eliminate the last hypothesis. $)
    reuunixfr $p |- ( E! x e. A ph -> U. { x e. A | ph } = C ) $=
      ( wreu cv crab wcel cuni wceq reuxfr reucl2 wb reucl hbrab1 hbuni rabxfr
      syl mpbird sylbi rabid baibr reubiia biimpi ax-17 wal wi hbel a1i eleq1
      reuuni2f syl11anc mpbid baib rabbiia unieqi syl5eqr ) ACFOZCPZACFQZRZCFQZ
      SZHVJSVHHVJRZVMHTZVHBDFOZVNABCDGFJNLUAZVPVNBDFQZSZVRRZBDFUBVPVSFRZVNVTUCB
      DFUDZABCDEGVSHFDEVRBDEFUEUFIJLMUGUHUIUJVHHFRZVKCFOZVNVOUCVHVPWCVQVPWAWCWB
      KUHUJVHWDAVKCFVKVIFRZAACFUKZULUMUNVKVNCEFHEPHRCUOZVNVNCUPUQWCCEEHVJWGACEF
      UEURUSVIHVJUTVAVBVCVLVJVKACFVKWEAWFVDVEVFVG $.
      $( [13-Jun-2005] $)
  $}

  $( The Axiom of Union and its converse.  A class is a set iff its union is a
     set. $)
  uniexb $p |- ( A e. _V <-> U. A e. _V ) $=
    ( cvv wcel cuni uniexg cpw wss ssexg pwuni pwexg sylancr impbii ) ABCZADZBC
    ZABEANFZGPBCMOAPBHAINBJKL $.
    $( [11-Nov-2003] $)

  $( The Axiom of Power Sets and its converse.  A class is a set iff its power
     class is a set. $)
  pwexb $p |- ( A e. _V <-> ~P A e. _V ) $=
    ( cpw cvv wcel cuni uniexb unipw eleq1i bitr2i ) ABZCDJEZCDACDJFKACAGHI $.
    $( [11-Nov-2003] $)

  $( The union of the universe is the universe.  Exercise 4.12(c) of
     [Mendelson] p. 235. $)
  univ $p |- U. _V = _V $=
    ( cvv cpw cuni pwv unieqi unipw eqtr3i ) ABZCACAHADEAFG $.
    $( [14-Sep-2003] $)

  ${
    eldifpw.1 $e |- C e. _V $.
    $( Membership in a power class difference. $)
    eldifpw $p |- ( ( A e. ~P B /\ -. C C_ B ) ->
                   ( A u. C ) e. ( ~P ( B u. C ) \ ~P B ) ) $=
      ( cpw wcel wss wn wa cun cdif elpwi cvv wb unexg mpan2 elpwg syl unss1
      syl5bir mpd unss simpr sylbir con3i anim12i eldif sylibr ) ABEZFZCBGZHZIA
      CJZBCJZEZFZUMUIFZHZIUMUOUIKFUJUPULURUJABGZUPABLUJUPUMUNGZUSUJUMMFZUPUTNUJ
      CMFVADACUIMOPUMUNMQRABCSTUAUQUKUQUMBGZUKUMBLVBUSUKIUKACBUBUSUKUCUDRUEUFUM
      UOUIUGUH $.
      $( [25-Mar-2007] $)

    $( Membership in the power class of a union. $)
    elpwun $p |- ( A e. ~P ( B u. C ) <-> ( A \ C ) e. ~P B ) $=
      ( cun cpw wcel cvv cdif elisset wb difex2 ax-mp sylibr wss elpwg difexg
      syl uncom sseq2i ssundif bitri syl6rbbr bitrd pm5.21nii ) ABCEZFZGZAHGZAC
      IZBFZGZAUGJULUJHGZUIUJUKJCHGUIUMKDACHLMNUIUHAUFOZULAUFHPUIULUJBOZUNUIUMUL
      UOKACHQUJBHPRUNACBEZOUOUFUPABCSTACBUAUBUCUDUE $.
      $( [26-Mar-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Membership in an extension of a power class. $)
    elpwunsn $p |- ( A e. ( ~P ( B u. { C } ) \ ~P B ) -> C e. A ) $=
      ( vx csn cun cpw cdif wcel wn wa eldif cv wrex wral wss elpwg dfss3
      syl6bb notbid biimpa rexnal sylibr wi wceq elpwi ssel wo elun elsni
      orim2i ord sylbi imim2i imp3a syl eleq1 biimpd 3syl exp3a com4r pm2.43b
      r19.23adv imp syldan ) ABCEZFZGZBGZHIAVHIZAVIIZJZKZCAIZAVHVILVJVLDMZBIZJZ
      DANZVNVMVPDAOZJZVRVJVLVTVJVKVSVJVKABPVSABVHQDABRSTUAVPDAUBUCVJVRVNVJVQVND
      AVJVOAIZVQVNUDVJWAVQWAVNVJWAVQWAVNUDZVJAVGPZWAVQKZVOCUEZUDZWDWBUDAVGUFWCW
      AVOVGIZUDZWFAVGVOUGWHWAVQWEWGVQWEUDZWAWGVPVOVFIZUHZWIVOBVFUIWKVPWEWJWEVPV
      OCUJUKULUMUNUOUPWEWBWDWEWAVNVOCAUQURUNUSUTVAVBVCVDVEUM $.
      $( [26-Mar-2007] $)
  $}

  ${
    op1stb.1 $e |- A e. _V $.
    $( Extract the first member of an ordered pair.  Theorem 73 of [Suppes]
       p. 42.  (See ~ op2ndb to extract the second member, ~ op1sta for an
       alternate version, and ~ op1st for the preferred version.) $)
    op1stb $p |- |^| |^| <. A , B >. = A $=
      ( cop cint csn cpr cin df-op inteqi snex prex intpr wss wceq snsspr1
      df-ss mpbi 3eqtri intsn eqtri ) ABDZEZEAFZEAUCUDUCUDABGZGZEUDUEHZUDUBUFAB
      IJUDUEAKABLMUDUENUGUDOABPUDUEQRSJACTUA $.
      $( [25-Nov-2003] $)
  $}

  ${
    $d x y A $.
    iunpw.1 $e |- A e. _V $.
    $( An indexed union of a power class in terms of the power class of the
       union of its index.  Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpw $p |- ( E. x e. A x = U. A <-> ~P U. A = U_ x e. A ~P x ) $=
      ( vy cv cuni wceq wrex cpw ciun wss wcel sseq2 biimprcd reximdv com12
      ssiun uniiun syl6ssr impbid1 visset elpw eliun df-pw abeq2i rexbii bitri
      3bitr4g eqrdv ssid eleq2 uniex syl5bbr mpbii sylib wa elssuni elpwi
      anim12i eqss sylibr ex reximia syl impbii ) AEZBFZGZABHZVGIZABVFIZJZGZVID
      VJVLVIDEZVGKZVNVFKZABHZVNVJLVNVLLZVIVOVQVOVIVQVOVHVPABVHVPVOVFVGVNMNOPVQV
      NABVFJVGABVFVNQABRSTVNVGDUAUBVRVNVKLZABHVQAVNBVKUCVSVPABVPDVKDVFUDUEUFUGU
      HUIVMVGVKLZABHZVIVMVGVLLZWAVMVGVGKZWBVGUJVMVGVJLWBWCVJVLVGUKVGVGBCULUBUMU
      NAVGBVKUCUOVTVHABVFBLZVTVHWDVTUPVFVGKZVGVFKZUPVHWDWEVTWFVFBUQVGVFURUSVFVG
      UTVAVBVCVDVE $.
      $( [29-Nov-2003] $)
  $}

  ${
    $d w v x $.  $d w v y $.  $d w v z $.  $d w v R $.  $d w v A $.
    $( A founded relation has no 3-cycle loops.  Special case of Proposition
       6.23 of [TakeutiZaring] p. 30. $)
    fr3nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x R y /\ y R z /\ z R x ) ) $=
      ( vw vv wfr cv ctp wss wbr w3a wn wcel wa cab cin c0 wceq wrex wne
      visset tpnz tpex frc mp3an3 w3o breq2 abbidv ineq2d neeq1d brab1 tpid3
      inelcm mpan sylbi syl5cbir tpid1 tpid2 3jaao eltp syl5ib com12 necon2bd
      r19.23aiv syl 3anrot tpss bitri sylan2b ) DEHZBIZCIZAIZJZDKZVOVMELZVMVNEL
      ZVNVOELZMZNZVODOZVMDOZVNDOZMZVLVQPVPFIZGIZELZFQZRZSTZGVPUAZWBVLVQVPSUBWMV
      MVNVOBUCZUDGFDVPEVMVNVOUEUFUGWLWBGVPWHVPOZWAWKSWAWOWKSUBZWAWHVMTZWHVNTZWH
      VOTZUHWPWOVRWQWPVSWRVTWSWQWPVPWGVMELZFQZRZSUBZVRWQWKXBSWQWJXAVPWQWIWTFWHV
      MWGEUIUJUKULVRVOXAOZXCAFVMEUMVOVPOXDXCVMVNVOAUCZUNVOVPXAUOUPUQURWRWPVPWGV
      NELZFQZRZSUBZVSWRWKXHSWRWJXGVPWRWIXFFWHVNWGEUIUJUKULVSVMXGOZXIBFVNEUMVMVP
      OXJXIVMVNVOWNUSVMVPXGUOUPUQURWSWPVPWGVOELZFQZRZSUBZVTWSWKXMSWSWJXLVPWSWIX
      KFWHVOWGEUIUJUKULVTVNXLOZXNCFVOEUMVNVPOXOXNVMVNVOCUCZUTVNVPXLUOUPUQURVAWH
      VMVNVOGUCVBVCVDVEVFVGWFWDWEWCMVQWCWDWEVHVMVNVODWNXPXEVIVJVK $.
      $( [10-Apr-1994] $)
  $}

  $( A set founded by epsilon contains no 3-cycle loops. $)
  epne3 $p |- ( ( _E Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x e. y /\ y e. z /\ z e. x ) ) $=
    ( cep wfr cv wcel w3a wa wbr wn fr3nr epel 3anbi123i notbii sylib ) DEFAGZD
    HBGZDHCGZDHIJRSEKZSTEKZTREKZIZLRSHZSTHZTRHZIZLABCDEMUDUHUAUEUBUFUCUGABNBCNC
    ANOPQ $.
    $( [19-Apr-1994] $)

  ${
    $d x y z R $.  $d x y z A $.
    $( Alternate definition of well-ordering.  Definition 6.24(2) of
       [TakeutiZaring] p. 30.  (The proof was shortened by Andrew Salmon,
       12-Aug-2011.) $)
    dfwe2 $p |- ( R We A <-> ( R Fr A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $=
      ( vz wwe wfr wor wa cv wbr wceq w3o wral df-we wpo simpr wn wi wcel w3a
      wal ax-1 a1i breq2 anbi2d notbid fr2nr 3adantr3 syl5cbi pm2.21 syl6
      fr3nr df-3an biimpri ancoms nsyl pm2.21d exp3a 3jaod frirr 3ad2antr1
      jctild ex a2d alimdv 2alimdv r3al 3imtr4g ralidm equequ2 breq1 3orbi123d
      cbvralv ralbii bitr3i df-po ancrd impbid2 df-so syl5bb pm5.32i bitri ) CD
      FCDGZCDHZIWDAJZBJZDKZWFWGLZWGWFDKZMZBCNZACNZICDOWDWEWMWDCDPZWMIZWMWEWDWOW
      MWNWMQWDWMWNWDWFEJZDKZWFWPLZWPWFDKZMZECNZBCNZACNZWFWFDKRZWHWGWPDKZIZWQSZI
      ZECNBCNACNZWMWNWDWFCTZWGCTZWPCTZUAZWTSZEUBZBUBAUBXMXHSZEUBZBUBAUBXCXIWDXO
      XQABWDXNXPEWDXMWTXHWDXMWTXHSWDXMIZWTXGXDXRWQXGWRWSWQXGSXRWQXFUCUDXRWRXFRZ
      XGWRWHWJIZRZXSXRWRXTXFWRWJXEWHWFWPWGDUEUFUGWDXJXKYAXLABCDUHUIUJXFWQUKULXR
      WSXFWQXRWSXFIZWQXRWHXEWSUAZYBABECDUMXFWSYCYCXFWSIWHXEWSUNUOUPUQURUSUTWDXK
      XJXDXLACDVAVBVCVDVEVFVGWTABECCCVHXHABECCCVHVIWLXBACWLWLBCNXBWKBCVJWLXABCW
      KWTBECWGWPLWHWQWIWRWJWSWGWPWFDUEBEAVKWGWPWFDVLVMVNVOVPVOABECDVQVIVRVSABCD
      VTWAWBWC $.
      $( [12-Aug-2011] $) $( [16-Mar-1997] $)
  $}


