$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Introduce the Axiom of Union
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z $.
    $( Axiom of Union.  An axiom of Zermelo-Fraenkel set theory.  It states
       that a set ` y ` exists that includes the union of a given set ` x `
       i.e. the collection of all members of the members of ` x ` .  The
       variant ~ axun2 states that the union itself exists.  A version with
       the standard abbreviation for union is ~ uniex2 .  A version using class
       notation is ~ uniex .

       The union of a class ~ df-uni should not be confused with the union of
       two classes ~ df-un .  Their relationship is shown in ~ unipr . $)
    ax-un $a |- E. y A. z ( E. w ( z e. w /\ w e. x ) -> z e. y ) $.

    $( Axiom of Union expressed with fewest number of different variables. $)
    axun $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wcel wa wex wi wal ax-un wceq elequ2 elequ1 anbi12d cbvexv
      imbi1i albii exbii mpbi ) BEZDEZFZUBCEZFZGZDHZUAAEZFZIZBJZAHUIUHUDFZGZAHZ
      UIIZBJZAHCABDKUKUPAUJUOBUGUNUIUFUMDAUBUHLUCUIUEULDABMDACNOPQRST $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Union ~ ax-un .  For any set ` x ` , there
       exists a set ` y ` whose members are exactly the members of the members
       of ` x ` i.e. the union of ` x ` .  Axiom Union of [BellMachover]
       p. 466. $)
    axun2 $p |- E. y A. z ( z e. y <-> E. w ( z e. w /\ w e. x ) ) $=
      ( cv wcel wa wex ax-un bm1.3ii ) CEDEZFKAEFGDHBCABCDIJ $.
      $( [4-Jun-2006] $)  $( [4-Jun-2006] $)

    $( The Axiom of Union using the standard abbreviation for union.  Given
       any set ` x ` , its union ` y ` exists. $)
    uniex2 $p |- E. y y = U. x $=
      ( vz cv cuni wceq wex wcel wb wal wi wa axun eluni imbi1i albii exbii
      mpbir bm1.3ii dfcleq ) BDZADZEZFZBGCDZUAHZUEUCHZICJZBGUGBCUGUFKZCJZBGUFUA
      UBHLBGZUFKZCJZBGBCAMUJUMBUIULCUGUKUFBUEUBNOPQRSUDUHBCUAUCTQR $.
      $( [4-Jun-2006] $) $( [4-Jun-2006] $)
  $}

  ${
    $d x y A $.
    uniex.1 $e |- A e. V $.
    $( The Axiom of Union in class notation.   This says that if ` A ` is
       a set i.e. ` A e. V ` (see ~ isset ), then the union of ` A ` is also a
       set.  Same as Axiom 3 of [TakeutiZaring] p. 16. $)
    uniex $p |- U. A e. V $=
      ( vx vy cv cuni cvv wcel wceq unieq eleq1d uniex2 issetri vtocl ) CEZFZGH
      AFZGHCABOAIPQGOAJKDPCDLMN $.
      $( [11-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( The ZF Axiom of Union in class notation, in the form of a theorem
       instead of an inference.  We use the antecedent ` A e. B ` instead of
       ` A e. V ` to make the theorem more general and thus shorten some
       proofs; obviously ` V ` is one possibility for ` B ` . $)
    uniexg $p |- ( A e. B -> U. A e. V ) $=
      ( vx cv cuni cvv wcel wceq unieq eleq1d visset uniex vtoclg ) CDZEZFGAEZF
      GCABNAHOPFNAIJNCKLM $.
      $( [25-Nov-1994] $)
  $}

  ${
    unex.1 $e |- A e. V $.
    unex.2 $e |- B e. V $.
    $( The union of two sets is a set.  Corollary 5.8 of [TakeutiZaring]
        p. 16. $)
    unex $p |- ( A u. B ) e. V $=
      ( cpr cuni cun cvv unipr prex uniex eqeltrr ) ABEZFABGHABCDIMABJKL $.
      $( [1-Jul-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Existence of union is equivalent to existence of its components. $)
    unexb $p |- ( ( A e. V /\ B e. V ) <-> ( A u. B ) e. V ) $=
      ( vx vy cvv wcel wa cun cv wceq uneq1 eleq1d uneq2 visset unex vtocl2g
      wss ssun1 ssexg mpan ssun2 jca impbi ) AEFZBEFZGABHZEFZCIZDIZHZEFAUIHZEFU
      GCDABEEUHAJUJUKEUHAUIKLUIBJUKUFEUIBAMLUHUICNDNOPUGUDUEAUFQUGUDABRAUFESTBU
      FQUGUEBAUABUFESTUBUC $.
      $( [11-Jun-1998] $)
  $}

  $( A union of two sets is a set.  Corollary 5.8 of [TakeutiZaring] p. 16. $)
  unexg $p |- ( ( A e. C /\ B e. D ) -> ( A u. B ) e. V ) $=
    ( cvv wcel cun wa unexb biimp elisset syl2an ) AEFZBEFZABGEFZACFBDFMNHOABIJ
    ACKBDKL $.
    $( [19-Sep-2006] $) $( [18-Sep-2006] $)

  ${
    $( A version of ~ unisn without the ` A e. V ` hypothesis.  (Contributed by
       Stefan Allan, 14-Mar-06.) $)
    unisn2 $p |- U. { A } e. { (/) , A } $=
      ( cvv wcel csn cuni c0 cpr unisng wceq wo wn eqid a1i orri elprg mpbiri
      eqeltrd snprc biimp unieqd uni0 0ex pri1 eqeltr syl6eqel pm2.61i ) ABCZAD
      ZEZFAGZCUGUIAUJABHUGAUJCAFIZAAIZJUKULULUKKALMNAFABOPQUGKZUIFEZUJUMUHFUMUH
      FIARSTUNFUJUAFAUBUCUDUEUF $.
      $( [14-Mar-2006] $) $( [14-Mar-2006] $)
  $}

  $( If the subtrahend of a class difference exists, then the minuend exists
     iff the difference exists. $)
  difex2 $p |- ( B e. C -> ( A e. V <-> ( A \ B ) e. V ) ) $=
    ( wcel cvv cdif difexg wa cun elisset anim1i ancoms unexb sylib undif2
    syl5eqelr wss ssun2 ssexg mpan syl expcom impbid2 ) BCDZAEDZABFZEDZABEGUGUD
    UEUGUDHZBAIZEDZUEUHBUFIZEUIUHBEDZUGHZUKEDUDUGUMUDULUGBCJKLBUFMNBAOPAUIQUJUE
    ABRAUIESTUAUBUC $.
    $( [12-Nov-2003] $) $( [12-Nov-2003] $)

  $( A triple of classes exists. $)
  tpex $p |- { A , B , C } e. V $=
    ( ctp cpr csn cun cvv df-tp prex snex unex eqeltr ) ABCDABEZCFZGHABCINOABJC
    KLM $.
    $( [10-Apr-1994] $)

  $( Each member of an ordered pair belongs to the union of the union of a
     class to which the ordered pair belongs.  Lemma 3D of [Enderton] p. 41. $)
  opeluu $p |- ( <. x , y >. e. A ->
               ( x e. U. U. A /\ y e. U. U. A ) ) $=
    ( cv cop wcel cuni cpr opi2 elunii mpan visset pri1 syl pri2 jca ) ADZBDZEZ
    CFZQCGZGZFZRUBFZTQRHZUAFZUCUESFTUFQRIUESCJKZQUEFUFUCQRALMQUEUAJKNTUFUDUGRUE
    FUFUDQRBLORUEUAJKNP $.
    $( [31-Mar-1995] $)

  ${
    $d x y z $.  $d y z ph $.
    $( If ` ph ` is true for exactly one ` x ` , then ` U. { x | ph } ` is a
       way to express "the unique element such that ` ph ` is true."   Some
       books use a special symbol such as iota to denote "the unique element
       such that." $)
    euuni $p |- ( E! x ph -> ( ph <-> U. { x | ph } = x ) ) $=
      ( vy vz weu cab cuni cv wceq wa wex wb cvv wcel euabex uniexg syl eueq
      eqcom eubii hbab1 hbuni ax-17 hbeq eqeq2 cbveu 3bitr sylib csn eusn
      visset snid eleq2 mpbiri abid unieq unisn syl6eq jca 19.22i sylbi
      eupickb mpd3an ) ABEZABFZGZBHZIZBEZAVHJZBKZAVHLVDVFMNZVIVDVEMNVLABOVEMPQV
      LCHZVFIZCEVFVMIZCEVICVFRVNVOCVMVFSTVOVHCBBDDVFVMBDVEABDUAUBDHVMNBUCUDVHCU
      CVMVGVFUEUFUGUHVDVEVGUIZIZBKVKABUJVQVJBVQAVHVQVGVENZAVQVRVGVPNVGBUKZULVEV
      PVGUMUNABUOUHVQVFVPGVGVEVPUPVGVSUQURUSUTVAAVHBVBVC $.
      $( [22-Feb-2004] $) $( [22-Feb-2004] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( A way to express "the unique element such that" (restricted
       quantifier version). $)
    reuuni1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
            ( ph <-> U. { x e. A | ph } = x ) ) $=
      ( cv wcel wa weu crab cuni wceq wb wreu cab wi euuni biimpd exp3a impcom
      pm3.27 syl6bir adantl impbid df-rab unieqi eqeq1i syl6bbr df-reu sylan2b
      ) BDZCEZUJAFZBGZAABCHZIZUIJZKABCLUJULFZAUKBMZIZUIJZUOUPAUSULUJAUSNULUJAUS
      ULUKUSUKBOZPQRULUSANUJULUSUKAUTUJASTUAUBUNURUIUMUQABCUCUDUEUFABCUGUH $.
      $( [3-Mar-2004] $) $( [25-Nov-1994] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    reuuni2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    reuuni2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    reuuni2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ."  This theorem shows a condition
       that allows us to represent this element with a class expression
       ` B `.  The second hypothesis is a weakened bound variable condition
       that allows ~ hbsbc1g to be used. $)
    reuuni2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( wcel wreu crab cuni wceq wb wi cv ax-17 hbel wal hbreu1 a1i hbrab1
      hbuni hbeq hbbid hbimd hbim1 eleq1 eqeq2 bibi12d imbi2d imbi12d reuuni1
      ex vtoclgf pm2.43i imp ) FEJZACEKZBACELZMZFNZOZUSUTVDPZCQZEJZUTAVBVFNZOZP
      ZPUSVEPCDFEGUSVECCDDFEGDQEJCRSZUSUTVDCVKUTUTCTPUSACEUAUBUSBVCCVKHVCVCCTPU
      SCDDVBFCDVAACDEUCUDGUEUBUFUGUHVFFNZVGUSVJVEVFFEUIVLVIVDUTVLABVHVCIVFFVBUJ
      UKULUMVGUTVIACEUNUOUPUQUR $.
      $( [27-May-2006] $) $( [19-Oct-2005] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reuuni2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ." $)
    reuuni2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( vy cv wcel ax-17 wal wi a1i reuuni2f ) ABCGDEGHEICJBBCKLEDIBCJMFN $.
      $( [27-May-2006] $) $( [28-Mar-1997] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Closure law for "the unique element in ` A ` such that ` ph ` ." $)
    reucl $p |- ( E! x e. A ph -> U. { x e. A | ph } e. A ) $=
      ( vy cv wcel wa weu cab cuni wreu crab csn wceq wex eusn hbab1 hbuni
      ax-17 hbel unieq visset unisn syl6req snid eleq2 mpbiri abid sylib
      pm3.26d eqeltrrd 19.23ai sylbi df-reu df-rab unieqi eleq1i 3imtr4 ) BEZCF
      ZAGZBHZVABIZJZCFZABCKABCLZJZCFVBVCUSMZNZBOVEVABPVIVEBBDDVDCBDVCVABDQRDECF
      BSTVIUSVDCVIVDVHJUSVCVHUAUSBUBZUCUDVIUTAVIUSVCFZVAVIVKUSVHFUSVJUEVCVHUSUF
      UGVABUHUIUJUKULUMABCUNVGVDCVFVCABCUOUPUQUR $.
      $( [22-Nov-1994] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.  $d x ch $.
    reuuni3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    reuuni3.2 $e |- ( x = U. { y e. A | ps } -> ( ph <-> ch ) ) $.
    $( Derive the property ` ch ` of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { y e. A | ps } ` . $)
    reuuni3 $p |- ( E! x e. A ph -> ch ) $=
      ( crab cuni wcel wreu reucl cbvrabv unieqi syl5eqelr wa wceq reuuni2
      mpbiri mpancom ) BEFIZJZFKZADFLZCUEADFIZJZFUCADFMUFUBABDEFGNOZPUDUEQCUGUC
      RUHACDFUCHSTUA $.
      $( [13-Nov-2004] $) $( [11-Nov-2004] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Derive the property of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { x e. A | ph } ` . $)
    reuuni4 $p |- ( E! x e. A ph -> [ U. { x e. A | ph } / x ] ph ) $=
      ( vy wreu crab cuni wcel wsbc reucl cvv wrex wi reurex hbreu1 hbrab1
      hbuni hbsbc1 cv wa wceq wb reuuni1 sbceq1a eqcoms syl6bi ibd expcom a1i
      com4l r19.23ad mpd elisset syl5 ) ABCEZABCFZGZCHZABUQIZABCJUOUQKHZUSURUOA
      BCLUTUSMZABCNUOAVABCABCOABDUQKBDUPABDCPQRUTUOBSZCHZAUSUOVCAUSMZMMUTVCUOVD
      VCUOTZAUSVEAUQVBUAAUSUBZABCUCVFVBUQABUQUDUEUFUGUHUIUJUKULUQCUMUNUL $.
      $( [23-Nov-2004] $) $( [11-Nov-2004] $)

    $( Membership law for "the unique element in ` A ` such that ` ph ` ." $)
    reucl2 $p |- ( E! x e. A ph -> U. { x e. A | ph } e. { x e. A | ph } ) $=
      ( vy wreu crab cuni wcel wsbc wa reucl reuuni4 jca cv ax-17 elrabsf
      sylibr ) ABCEZABCFZGZCHZABTIZJTSHRUAUBABCKABCLMABDTCDNCHBOPQ $.
      $( [12-Jun-2005] $) $( [11-Jun-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss $p |- ( ( A (_ B /\ E. x e. A ph /\ E! x e. B ph ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( vy wss wrex wreu w3a crab cuni wsbc wceq reuss reuuni4 syl wcel wb
      hbrab1 hbuni hbsbc1g sbceq1a reuuni2f reucl wi ssel 3ad2ant1 mpd 3simp3
      sylanc mpbid eqcomd ) CDFZABCGZABDHZIZABDJKZABCJZKZUPABUSLZUQUSMZUPABCHZU
      TABCDNZABCOPUSDQZUOUTVARUPAUTBEDUSBEURABECSTZABEUSDVEUAABUSUBUCUPUSCQZVDU
      PVBVFVCABCUDPUMUNVFVDUEUOCDUSUFUGUHUMUNUOUIUJUKUL $.
      $( [21-Nov-2005] $) $( [19-Oct-2005] $)

    $( Restriction of a unique element to a smaller class. $)
    mouniss $p |- ( ( A (_ B /\ E. x e. A ph /\ E* x ( x e. B /\ ph ) ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( wss wrex cv wcel wa wmo crab cuni wceq wreu ssel anim1d r19.22dv2 imp
      anim1i reu5 sylibr reuuniss 3expa syldan 3impa ) CDEZABCFZBGZDHZAIBJZABCK
      LABDKLMZUFUGIZUJABDNZUKULUJIABDFZUJIUMULUNUJUFUGUNUFAABCDUFUHCHUIACDUHOPQ
      RSABDTUAUFUGUMUKABCDUBUCUDUE $.
      $( [26-Feb-2006] $) $( [19-Feb-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss2 $p |- ( ( ( A (_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) ) -> U. { x e. A | ph } = U. { x e. B | ps } ) $=
      ( vy wss wi wral wa wrex wreu crab cuni wsbc wceq reuuni4 wcel reucl
      ra4sbc sbcimg sylibd syl mpid reuss2 simplr sylc wb hbrab1 hbuni hbsbc1g
      sbceq1a reuuni2f ssel ad2antrr mpd simprr sylanc mpbid eqcomd ) DEGZABHZC
      DIZJZACDKZBCELZJZJZBCEMNZACDMZNZVHBCVKOZVIVKPZACDLZVCVLVHVNVCACVKOZVLACDQ
      VNVKDRZVCVOVLHZHACDSZVPVCVBCVKOVQVBCVKDTABCVKDUAUBUCUDABCDEUEZVAVCVGUFUGV
      KERZVFVLVMUHVHBVLCFEVKCFVJACFDUIUJZBCFVKEWAUKBCVKULUMVHVPVTVHVNVPVSVRUCVA
      VPVTHVCVGDEVKUNUOUPVDVEVFUQURUSUT $.
      $( [8-Dec-2005] $) $( [20-Oct-2005] $)
  $}

  ${
    $d y z A $.  $d y z ph $.  $d x y z $.
    $( A way to express restricted existential uniqueness of a wff:  its
       restricted class abstraction is a singleton. $)
    reusn $p |- ( E! x e. A ph <-> E. y { x e. A | ph } = { y } ) $=
      ( vz wreu cv wcel wa weu cab csn wceq wex crab df-reu eusn df-rab eqeq1i
      exbii ax-17 hbrab1 hbeq sneq eqeq2d cbvex bitr3 3bitr ) ABDFBGZDHAIZBJUJB
      KZUILZMZBNZABDOZCGZLZMZCNZABDPUJBQUNUOULMZBNUSUTUMBUOUKULABDRSTUTURBCUTCU
      ABEEUOUQABEDUBEGUQHBUAUCUIUPMULUQUOUIUPUDUEUFUGUH $.
      $( [30-May-2006] $) $( [30-May-2006] $)
  $}

  ${
    $d y A $. $d y B $.  $d y ph $.  $d x y $.
    reusni.1 $e |- B e. V $.
    $( Restricted existential uniqueness determined by a singleton. $)
    reusni $p |- ( { x e. A | ph } = { B } -> E! x e. A ph ) $=
      ( vy crab csn wceq cv wex wreu sneq eqeq2d cla4ev reusn sylibr ) ABCGZDHZ
      IZRFJZHZIZFKABCLUCTFDEUADIUBSRUADMNOABFCPQ $.
      $( [19-Jun-2006] $) $( [29-May-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rabsnt.1 $e |- B e. V $.
    rabsnt.2 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Truth implied by equality of a restricted class abstraction and a
       singleton. $)
    rabsnt $p |- ( { x e. A | ph } = { B } -> ps ) $=
      ( wcel wreu wa crab cuni wceq csn reuuni2 biimprd wss ssrab2 sseq1 mpbii
      snss sylibr reusni jca unieq unisn syl6eq sylc ) EDHZACDIZJZACDKZLZEMZBUL
      ENZMZUKBUNABCDEGOPUPUIUJUPUODQZUIUPULDQUQACDRULUODSTEDFUAUBACDEFUCUDUPUMU
      OLEULUOUEEFUFUGUH $.
      $( [7-Dec-2006] $) $( [29-May-2006] $)
  $}

  ${
    $d y A $.  $d y ph $.  $d x y $.
    $( A restricted class abstraction with a unique member can be expressed as
       a singleton. $)
    reuunisn $p |- ( E! x e. A ph ->
                   { x e. A | ph } = { U. { x e. A | ph } } ) $=
      ( vy wreu crab cv csn wceq wex cuni reusn unieq visset unisn syl6eq
      sneqd eqtr3t mpdan 19.23aiv sylbi ) ABCEABCFZDGZHZIZDJUBUBKZHZIZABDCLUEUH
      DUEUGUDIUHUEUFUCUEUFUDKUCUBUDMUCDNOPQUBUGUDRSTUA $.
      $( [24-Jun-2006] $) $( [30-May-2006] $)
  $}

  ${
    $d x A $.  $d y ph $.  $d x ps $.  $d x y $.
    alxfr.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    alxfr $p |- ( ( A. y A e. B /\ A. x E. y x = A ) ->
                ( A. x ph <-> A. y ps ) ) $=
      ( wcel wal cv wceq wex wa wi hba1 ax-17 cla4gv a4s 19.21ad adantr hbim
      ax-4 syl5bir 19.23ai adantl impbid ) EFHZDIZCJEKZDLZCIZMACIZBDIZUHULUMNUK
      UHULBDUGDOULDPUGULBNDABCEFGQRSTUKUMULNUHUKUMACUJCOUMCPUJUMANZCUIUNDUMADBD
      OADPUAUIABUMGBDUBUCUDRSUEUF $.
      $( [?] $) $( [18-Feb-2007] $)
  $}

  ${
    $d x y ph $.  $d x ch $.  $d y ps $.  $d x A $.  $d x y B $.
    ralxfrd.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    ralxfrd.2 $e |- ( ( ph /\ x e. B ) -> E. y e. B x = A ) $.
    ralxfrd.3 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrd $p |- ( ph -> ( A. x e. B ps <-> A. y e. B ch ) ) $=
      ( wral cv wcel wi ex rcla4dv syld com23 r19.21adv wceq wrex wa ax-17
      hbra1 hban ra4 biimprd sylan9r r19.23ad syl5d impbid ) ABDGKZCEGKZAULCEGA
      ELGMZULCAUNFGMZULCNZAUNUOHOAUOUPABCDFGJPOQRSAUMBDGAUMDLZFTZEGUAZBUQGMZAUM
      USBNAUMUBURBEGAUMEAEUCCEGUDUEBEUCUMUNCAURBNCEGUFAURCBAURCBNAURUBBCJUGORUH
      UIOAUTUSIOUJSUK $.
      $( [18-Feb-2007] $) $( [17-Feb-2007] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    ralxfr.1 $e |- ( y e. B -> A e. B ) $.
    ralxfr.2 $e |- ( x e. B -> E. y e. B x = A ) $.
    ralxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfr $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( wral cv wcel wi rcla4v syl com12 r19.21aiv wceq wrex hbra1 ax-17 ra4
      biimprcd syl6 r19.23ad syl5 impbi ) ACFJZBDFJZUHBDFDKFLZUHBUJEFLUHBMGABCE
      FINOPQUIACFUICKZERZDFSAUKFLUIULADFBDFTADUAUIUJBULAMBDFUBULABIUCUDUEHUFQUG
      $.
      $( [13-Jun-2005] $) $( [10-Jun-2005] $)

    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrALT $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( cvv wceq wral wb eqid cv wcel adantl wrex ralxfrd ax-mp ) JJKZACFLBDFLM
      JNUAABCDEFDOFPEFPUAGQCOZFPUBEKZDFRUAHQUCABMUAIQST $.
      $( [13-Jun-2005] $) $( [10-Jun-2005] $)

    $( Transfer existence from a variable ` x ` to another variable ` y `
       contained in expression ` A ` . $)
    rexxfr $p |- ( E. x e. B ph <-> E. y e. B ps ) $=
      ( wn wral wrex cv wceq negbid ralxfr negbii dfrex2 3bitr4 ) AJZCFKZJBJZDF
      KZJACFLBDFLUAUCTUBCDEFGHCMENABIOPQACFRBDFRS $.
      $( [29-Jun-2005] $) $( [10-Jun-2005] $)
  $}

  ${
    $d x A $.  $d z B $.  $d z C $.  $d x y z D $.  $d y z ph $.  $d x z ps $.
    rabxfr.1 $e |- ( z e. B -> A. y z e. B ) $.
    rabxfr.2 $e |- ( z e. C -> A. y z e. C ) $.
    rabxfr.3 $e |- ( y e. D -> A e. D ) $.
    rabxfr.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    rabxfr.5 $e |- ( y = B -> A = C ) $.
    $( Abstraction class membership after substituting an expression ` A `
       (containing ` y ` ) for ` x ` in the class expression ` ph ` . $)
    rabxfr $p |- ( B e. D ->
                 ( C e. { x e. D | ph } <-> B e. { y e. D | ps } ) ) $=
      ( wcel crab wb wi wa cv ibibr mpbi anbi1d elrab rabid 3bitr4g rabbii
      eleq2i ax-17 hbel wceq eleq1d elrabf hbrab1 eleq1 3bitr3 pm5.32 mpbir )
      GIOZHACIPZOZGBDIPZOZQRUSVASZUSVCSZQGFUTOZDIPZOGDTZVBOZDIPZOVDVEVGVJGVFVID
      IVHIOZFIOZBSVKBSVFVIVKVLVKBVKVLRVKVLVKQRLVKVLUAUBUCABCFIMUDBDIUEUFUGUHVFV
      ADEGIJETZIODUIZDEEHUTKVMUTODUIUJVHGUKFHUTNULUMVIVCDEGIJVNDEEGVBJBDEIUNUJV
      HGVBUOUMUPUSVAVCUQUR $.
      $( [21-Jun-2005] $) $( [10-Jun-2005] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x y B $.
    reuxfr2.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr2.2 $e |- ( x e. B -> E* y ( y e. B /\ x = A ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    reuxfr2 $p |- ( E! x e. B E. y e. B ( x = A /\ ph ) <-> E! y e. B ph ) $=
      ( cv wceq wa wrex wreu wcel wmo wi 2reuswap moan syl ancom anass bitr
      mobii sylib mprg moeq moani an12 mpbi a1i impbi wb pm4.2i ceqsrexv
      reubiia ) BHZDIZAJZCEKBELZUQBEKZCELZACELURUTCHEMZUQJZCNZURUTOBEUQBCEPUOEM
      ZAVAUPJZJZCNZVCVDVECNVGGVEACQRVFVBCVFVEAJVBAVESVAUPATUAUBUCUDVDUQJZBNZUTU
      ROCEUQCBEPVIVAVDAJZUPJZBNVIUPVJBBDUEUFVKVHBVKUPVJJVHVJUPSUPVDAUGUAUBUHUIU
      DUJUSACEVADEMUSAUKFAABDEUPAULUMRUNUA $.
      $( [17-Nov-2004] $) $( [14-Nov-2004] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    reuxfr.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr.2 $e |- ( x e. B -> E! y e. B x = A ) $.
    reuxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  Use ~ reuhyp to
       eliminate the second hypothesis. $)
    reuxfr $p |- ( E! x e. B ph <-> E! y e. B ps ) $=
      ( wreu cv wceq wa wrex wcel reurex syl biantrurd r19.41v pm5.32i rexbii
      bitr3 syl6bb reubiia wmo weu df-reu eumo sylbi reuxfr2 bitr ) ACFJCKZELZB
      MZDFNZCFJBDFJAUOCFULFOZAUMDFNZAMZUOUPUQAUPUMDFJZUQHUMDFPQRURUMAMZDFNUOUMA
      DFSUTUNDFUMABITUAUBUCUDBCDEFGUPUSDKFOUMMZDUEZHUSVADUFVBUMDFUGVADUHUIQUJUK
      $.
      $( [23-Nov-2004] $) $( [14-Nov-2004] $)
  $}

  ${
    $d y B $.  $d y C $.  $d x y $.
    reuhyp.1 $e |- ( x e. C -> B e. C ) $.
    reuhyp.2 $e |- ( ( x e. C /\ y e. C ) -> ( x = A <-> y = B ) ) $.
    $( A theorem useful for eliminating the restricted existential uniqueness
       hypotheses in ~ reuxfr . $)
    reuhyp $p |- ( x e. C -> E! y e. C x = A ) $=
      ( cv wcel wceq wa weu wreu cvv elisset syl eueq sylib eleq1 syl5bir
      com12 pm4.71rd pm5.32da bitr4d eubidv mpbid df-reu sylibr ) AHZEIZBHZEIZU
      ICJZKZBLZUMBEMUJUKDJZBLZUOUJDNIZUQUJDEIZURFDEOPBDQRUJUPUNBUJUPULUPKUNUJUP
      ULUPUJULUPULUSUJUKDESFTUAUBUJULUMUPGUCUDUEUFUMBEUGUH $.
      $( [23-Nov-2004] $) $( [15-Nov-2004] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d ph y z $.  $d ps x z $.
    reuunixfr.1 $e |- ( z e. C -> A. y z e. C ) $.
    reuunixfr.2 $e |- ( y e. A -> B e. A ) $.
    reuunixfr.3 $e |- ( U. { y e. A | ps } e. A -> C e. A ) $.
    reuunixfr.4 $e |- ( x = B -> ( ph <-> ps ) ) $.
    reuunixfr.5 $e |- ( y = U. { y e. A | ps } -> B = C ) $.
    reuunixfr.6 $e |- ( x e. A -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` A `
       such that ` ph ` " to another variable ` y ` contained in expression
       ` B ` .  Use ~ reuhyp to eliminate the last hypothesis. $)
    reuunixfr $p |- ( E! x e. A ph -> U. { x e. A | ph } = C ) $=
      ( wreu cv crab wcel cuni wceq reuxfr reucl2 wb reucl hbrab1 hbuni rabxfr
      syl mpbird sylbi ax-17 wal wi hbel a1i eleq1 reuuni2f rabid baibr
      reubiia biimp sylanc mpbid baib rabbii unieqi syl5eqr ) ACFOZCPZACFQZRZCF
      QZSZHVJSVHHVJRZVMHTZVHBDFOZVNABCDGFJNLUAZVPVNBDFQZSZVRRZBDFUBVPVSFRZVNVTU
      CBDFUDZABCDEGVSHFDEVRBDEFUEUFIJLMUGUHUIUJHFRZVKCFOZVNVOUCVHVKVNCEFHEPHRCU
      KZVNVNCULUMWCCEEHVJWEACEFUEUNUOVIHVJUPUQVHVPWCVQVPWAWCWBKUHUJVHWDAVKCFVKV
      IFRZAACFURZUSUTVAVBVCVLVJVKACFVKWFAWGVDVEVFVG $.
      $( [25-Jun-2005] $) $( [13-Jun-2005] $)
  $}

  $( The Axiom of Union and its converse.  A class is a set iff its union is a
     set. $)
  uniexb $p |- ( A e. V <-> U. A e. V ) $=
    ( cvv wcel cuni uniexg cpw wss pwuni ssexg pwexg sylan2 mpan impbi ) ABCZAD
    ZBCZABEAOFZGZPNAHRQBCNPAQBIOBJKLM $.
    $( [17-Nov-2003] $) $( [11-Nov-2003] $)

  $( The Axiom of Power Sets and its converse.  A class is a set iff its power
     class is a set. $)
  pwexb $p |- ( A e. V <-> P~ A e. V ) $=
    ( cpw cvv wcel cuni uniexb unipw eleq1i bitr2 ) ABZCDJEZCDACDJFKACAGHI $.
    $( [17-Nov-2003] $) $( [11-Nov-2003] $)

  $( The union of the universe is the universe.  Exercise 4.12(c) of
     [Mendelson] p. 235. $)
  univ $p |- U. V = V $=
    ( cvv cpw cuni pwv unieqi unipw eqtr3 ) ABZCACAHADEAFG $.
    $( [14-Sep-2003] $)

  ${
    eldifpw.1 $e |- C e. V $.
    $( Membership in a power class difference. $)
    eldifpw $p |- ( ( A e. P~ B /\ -. C (_ B ) ->
                   ( A u. C ) e. ( P~ ( B u. C ) \ P~ B ) ) $=
      ( cpw wcel wss wn wa cun cdif elpwi cvv wb unexg mpan2 elpwg syl unss1
      syl5bir mpd unss pm3.27 sylbir con3i anim12i eldif sylibr ) ABEZFZCBGZHZI
      ACJZBCJZEZFZUMUIFZHZIUMUOUIKFUJUPULURUJABGZUPABLUJUPUMUNGZUSUJUMMFZUPUTNU
      JCMFVADACUIMOPUMUNMQRABCSTUAUQUKUQUMBGZUKUMBLVBUSUKIUKACBUBUSUKUCUDRUEUFU
      MUOUIUGUH $.
      $( [?] $) $( [25-Mar-2007] $)

    $( Membership in the power class of a union. $)
    elpwun $p |- ( A e. P~ ( B u. C ) <-> ( A \ C ) e. P~ B ) $=
      ( cun cpw wcel cvv cdif elisset wb difex2 ax-mp sylibr wss elpwg difexg
      syl uncom sseq2i ssundif bitr syl6rbbr bitrd pm5.21nii ) ABCEZFZGZAHGZACI
      ZBFZGZAUGJULUJHGZUIUJUKJCHGUIUMKDACHLMNUIUHAUFOZULAUFHPUIULUJBOZUNUIUMULU
      OKACHQUJBHPRUNACBEZOUOUFUPABCSTACBUAUBUCUDUE $.
      $( [29-Mar-2007] $) $( [26-Mar-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Membership in an extension of a power class. $)
    elpwunsn $p |- ( A e. ( P~ ( B u. { C } ) \ P~ B ) -> C e. A ) $=
      ( vx csn cun cpw cdif wcel wn wa eldif cv wrex wral wss elpwg dfss3
      syl6bb negbid biimpa rexnal sylibr wi wceq elpwi ssel wo elun elsni
      orim2i ord sylbi imim2i imp3a syl eleq1 biimpd 3syl exp3a com4r pm2.43b
      r19.23adv imp syldan ) ABCEZFZGZBGZHIAVHIZAVIIZJZKZCAIZAVHVILVJVLDMZBIZJZ
      DANZVNVMVPDAOZJZVRVJVLVTVJVKVSVJVKABPVSABVHQDABRSTUAVPDAUBUCVJVRVNVJVQVND
      AVJVOAIZVQVNUDVJWAVQWAVNVJWAVQWAVNUDZVJAVGPZWAVQKZVOCUEZUDZWDWBUDAVGUFWCW
      AVOVGIZUDZWFAVGVOUGWHWAVQWEWGVQWEUDZWAWGVPVOVFIZUHZWIVOBVFUIWKVPWEWJWEVPV
      OCUJUKULUMUNUOUPWEWBWDWEWAVNVOCAUQURUNUSUTVAVBVCVDVEUM $.
      $( [29-Mar-2007] $) $( [26-Mar-2007] $)
  $}

  ${
    op1stb.1 $e |- A e. V $.
    $( Extract the first member of an ordered pair.  Theorem 73 of [Suppes]
       p. 42.  (See ~ op2ndb to extract the second member, ~ op1sta for an
       alternate version, and ~ op1st for the preferred version.) $)
    op1stb $p |- |^| |^| <. A , B >. = A $=
      ( cop cint csn cpr cin df-op inteqi snex prex intpr wss wceq snsspr
      df-ss mpbi 3eqtr intsn eqtr ) ABDZEZEAFZEAUCUDUCUDABGZGZEUDUEHZUDUBUFABIJ
      UDUEAKABLMUDUENUGUDOABPUDUEQRSJACTUA $.
      $( [25-Nov-2003] $) $( [25-Nov-2003] $)
  $}

  ${
    $d x y A $.
    iunpw.1 $e |- A e. V $.
    $( The power class of an intersection in terms of indexed intersection.
       Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpw $p |- ( E. x e. A x = U. A <-> P~ U. A = U_ x e. A P~ x ) $=
      ( vy cv cuni wceq wrex cpw ciun wss wcel sseq2 biimprcd r19.22sdv com12
      ssiun uniiun syl6ssr impbid1 visset elpw eliun df-pw abeq2i rexbii bitr
      3bitr4g eqrdv ssid eleq2 uniex syl5bbr mpbii sylib wa elssuni elpwi
      anim12i eqss sylibr ex r19.22i syl impbi ) AEZBFZGZABHZVGIZABVFIZJZGZVIDV
      JVLVIDEZVGKZVNVFKZABHZVNVJLVNVLLZVIVOVQVOVIVQVOVHVPABVHVPVOVFVGVNMNOPVQVN
      ABVFJVGABVFVNQABRSTVNVGDUAUBVRVNVKLZABHVQAVNBVKUCVSVPABVPDVKDVFUDUEUFUGUH
      UIVMVGVKLZABHZVIVMVGVLLZWAVMVGVGKZWBVGUJVMVGVJLWBWCVJVLVGUKVGVGBCULUBUMUN
      AVGBVKUCUOVTVHABVFBLZVTVHWDVTUPVFVGKZVGVFKZUPVHWDWEVTWFVFBUQVGVFURUSVFVGU
      TVAVBVCVDVE $.
      $( [5-Dec-2003] $) $( [29-Nov-2003] $)
  $}


