$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Ordinal isomorphism, Hartog's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f w x y z A $.  $d f w x y z B $.
    $( Order-isomorphic ordinal numbers are equal.  (Contributed by Jeff
       Hankins, 16-Oct-2009.) $)
    ordiso $p |- ( ( A e. On /\ B e. On ) -> ( A = B
                <-> E. f f Isom _E , _E ( A , B ) ) ) $=
      ( vx vy vz vw con0 wcel wa wceq cep cv wiso wex w3a cid cres cvv resiexg
      3ad2ant1 wf1o wbr cfv wb wral df-iso f1oi f1oeq3 3ad2ant3 mpbii fvresi
      ad2antrl ad2antll breq12d bicomd ex r19.21aivv sylanbrc isoeq1 cla4egv
      sylc 3expia wn wrex wi crn simpllr ad2antrr simpr wf isof1o f1of syl
      ad2antlr simplrr ffvelrn syl11anc jca ontr1 wfo f1ofo forn 3syl eleqtrrd
      wfn f1ofn fvelrnb mpbid simprrr simprl eqeltrd fvex epelc sylibr simplr
      simprrl isorel syl12anc mpbird epel sylib simprr eleq1 fveq2 id eqeq12d
      imbi12d rcla4v exp3a com12 pm2.27 syl8 com24 syld com14 imp42 adantllr
      adantrl eqtr3d expr mpd r19.23adv imp44 adantlr biimpri ancomsd imp
      adantrll simprlr eqeltrrd impbid eqrdv exp43 r19.21aiv iman ralbii
      imbi12i imnan ancom notbii 3bitri ralnex bitri onelon adantrd ancrd
      reximdv2 notbid anbi12d onminex syl6 rexnal syl5ibr mt3d rcla4cva
      adantll sylan adantlrr eleq2d bitr3d a1i simpl adantl syldd com23 impr
      sylbid 19.23adv ) AHIZBHIZJZABKZABLLCMZNZCOZUVJUVKUVMUVPUVJUVKUVMPZQARZSI
      ZABLLUVRNZUVPUVJUVKUVSUVMAHTUAUVTABUVRUBZDMZEMZLUCZUWBUVRUDZUWCUVRUDZLUCZ
      UEZEAUFDAUFUVQDEABLLUVRUGUVQAAUVRUBZUWAAUHUVMUVJUWIUWAUEUVKABAUVRUIUJUKUV
      QUWHDEAAUVQUWBAIZUWCAIZJZUWHUVQUWLJZUWGUWDUWMUWEUWBUWFUWCLUWJUWEUWBKUVQUW
      KAUWBULUMUWKUWFUWCKUVQUWJAUWCULUNUOUPUQURUSUVOUVTCUVRSABLLUVRUVNUTVAVBVCU
      VLUVOUVMCUVLUVOUVMUVLUVOJZUWBUVNUDZUWBKZDAUFZUVMUWNUWQUWJUWPVDZJZUWKUWCUV
      NUDZUWCKZVDZJZVDZEUWBUFZJZDHVEZUWNUWKUXAVFZEUWBUFZUWJUWPVFZVFZDHUFZUXGVDZ
      UWNUXKDHUWNUWBHIZUXIUWJUWPUWNUXNJZUXIUWJJZJZFUWOUWBUXQFMZUWOIZUXRUWBIZUXQ
      UXSUXTUXQUXSJZGMZUVNUDZUXRKZGAVEZUXTUYAUXRUVNVGZIZUYEUYAUXRBUYFUYAUVKUXSU
      WOBIZJUXRBIZUXOUVKUXPUXSUVJUVKUVOUXNVHVIUYAUXSUYHUXQUXSVJUYAABUVNVKZUWJUY
      HUXOUYJUXPUXSUVOUYJUVLUXNUVOABUVNUBZUYJABLLUVNVLZABUVNVMVNZVOVIUXOUXIUWJU
      XSVPABUWBUVNVQVRVSUXRUWOBVTVBUXOUYFBKZUXPUXSUVOUYNUVLUXNUVOUYKABUVNWAUYNU
      YLABUVNWBABUVNWCWDZVOVIWEUXOUYGUYEUEZUXPUXSUVOUYPUVLUXNUVOUYKUVNAWFUYPUYL
      ABUVNWGGAUXRUVNWHWDZVOVIWIUYAUYDUXTGAUYAUYBAIZUYDUXTUXQUXSUYRUYDJZUXTUXQU
      XSUYSJZJZUYBUWBIZUXTVUAUYBUWBLUCZVUBVUAVUCUYCUWOLUCZVUAUYCUWOIVUDVUAUYCUX
      RUWOUXQUXSUYRUYDWJUXQUXSUYSWKWLUYCUWOUYBUVNWMUWBUVNWMZWNWOVUAUVOUYRUWJVUC
      VUDUEUXOUVOUXPUYTUVLUVOUXNWPVIUXQUXSUYRUYDWQUXOUXIUWJUYTVPABUYBUWBLLUVNWR
      WSWTGDXAXBUXQUYTVUBUXTUXQUYTVUBJJZUXRUYBUWBVUFUYCUXRUYBUYTUYDUXQVUBUXSUYR
      UYDXCUMUXQVUBUYCUYBKZUYTUWNUXPVUBVUGUXNUVLUXPVUBVUGUVOUVJUXPVUBVUGUVKUVJU
      XIUWJVUBVUGVUBUXIUWJUVJVUGVUBUXIUYRVUGVFZUWJUVJVUGVFVFUXHVUHEUYBUWBUWCUYB
      KZUWKUYRUXAVUGUWCUYBAXDVUIUWTUYCUWCUYBUWCUYBUVNXEVUIXFXGXHXIVUBUVJUWJVUHV
      UGVUBUVJUWJUYRVUHVUGVFUVJVUBUWJUYRVFUVJVUBUWJUYRUYBUWBAVTXJXKUYRVUGXLXMXN
      XOXPXQXRXRXRXSXTUXQUYTVUBXCWLYAYBYAXJYCYBUQUXOUXPUXTUXSUXOUXPUXTJZJZUXRUV
      NUDZUXRUWOUWNVUJVULUXRKZUXNUVLVUJVUMUVOUVJVUJVUMUVKUVJUXIUWJUXTVUMUXTUXIU
      WJUVJVUMUXTUXIUXRAIZVUMVFZUWJUVJVUMVFVFUXHVUOEUXRUWBUWCUXRKZUWKVUNUXAVUMU
      WCUXRAXDVUPUWTVULUWCUXRUWCUXRUVNXEVUPXFXGXHXIUXTUVJUWJVUOVUMUXTUVJUWJVUNV
      UOVUMVFUVJUXTUWJVUNVFUVJUXTUWJVUNUXRUWBAVTZXJXKVUNVUMXLXMXNXOXPYDYEYEYEVU
      KVULUWOLUCZVULUWOIVUKUXRUWBLUCZVURUXTVUSUXOUXPVUSUXTFDXAYFUNVUKUVOVUNUWJV
      USVURUEUVLUVOUXNVUJVHUXOUWJUXTVUNUXIUWNUWJUXTJZVUNUXNUVLVUTVUNUVOUVJVUTVU
      NUVKUVJVUTVUNUVJUXTUWJVUNVUQYGYHYEYEYEYIUXOUXIUWJUXTYJABUXRUWBLLUVNWRWSWI
      VULUWOUXRUVNWMVUEWNXBYKYAYLYMYNYOUXLUXFVDZDHUFUXMUXKVVADHUXKUXEUWSVDZVFUX
      EUWSJZVDVVAUXIUXEUXJVVBUXHUXDEUWBUWKUXAYPYQUWJUWPYPYRUXEUWSYSVVCUXFUXEUWS
      YTUUAUUBYQUXFDHUUCUUDXBUWNUWRDAVEZUXGUWQVDUWNVVDUWSDHVEUXGUWNUWRUWSDAHUWN
      UWSUXNUVJUWSUXNVFUVKUVOUVJUWJUXNUWRUVJUWJUXNAUWBUUEUQUUFVIUUGUUHUWSUXCDEU
      WBUWCKZUWJUWKUWRUXBUWBUWCAXDVVEUWPUXAVVEUWOUWTUWBUWCUWBUWCUVNXEVVEXFXGUUI
      UUJUUKUULUWPDAUUMUUNUUOUVLUVOUWQUVMUVLUVOUWQJZJZFABVVGVUNUYIVVGVUNUYIVVGV
      UNJVULUXRBVVFVUNVUMUVLUWQVUNVUMUVOUWPVUMDUXRAUWBUXRKZUWOVULUWBUXRUWBUXRUV
      NXEVVHXFXGUUPUUQUUQUVLUVOVUNVULBIZUWQUVOVUNVVIUVLUYJVUNVVIUVOABUXRUVNVQUY
      MUURUUQUUSYKUQVVGUYIUYEVUNVVGUYGUYIUYEVVGUYFBUXRUVOUYNUVLUWQUYOUMUUTUVOUY
      PUVLUWQUYQUMUVAVVGUYDVUNGAUVLUVOUWQUYRUYDVUNVFZVFUWNUYRUWQVVJUWNUYRUWQVUG
      VVJUYRUWQVUGVFVFUWNUWPVUGDUYBAUWBUYBKZUWOUYCUWBUYBUWBUYBUVNXEVVKXFXGXIUVB
      UWNUYRVUGUYDVUNUWNUYRJZVUGUYDJZJUXRUYBAVVMUXRUYBKVVLVVMUYCUXRUYBVUGUYDVJV
      UGUYDUVCXTUVDUWNUYRVVMWPWLYNUVEUVFUVGYCUVHYLYMYAYBUQUVIYL $.
      $( [16-Oct-2009] $)
  $}

  ${
    $d a b c f g h j k m n o p q r s t u v w x y z A $.
    $d a b c f h m n o p q x y z B $.  $d c m n o p q u C $.
    $d h j m n o p q r u v w y z D $.
    $d b c h j m n o p q r s u v w x y z F $.  $d b c h m n o p q G $.
    $d a f g h j m n o p q r s u v w x z H $.
    $d a b c f g h j k m n o p q r s t u v w x y z R $.
    ${
      ordtypelem.1 $e |- A e. _V $.
      ordtypelem.2 $e |- B = { h | E. b e. On ( h Fn b /\ A. c e. b ( h ` c ) =
      ( G ` ( h |` c ) ) ) } $.
      ordtypelem.3 $e |- F = U. B $.
      ordtypelem.4 $e |- C = { w e. A | A. j e. ran h j R w } $.
      ordtypelem.5 $e |- D = { w e. A | A. j e. ( F " x ) j R w } $.
      ordtypelem.6 $e |- G = { <. h , c >. | c = U. { v e. C | A. u e. C -. u R
      v } } $.
      ordtypelem.7 $e |- H = { w e. A | A. j e. ( F " y ) j R w } $.
      $( Lemma for ~ ordtype . $)
      ordtypelem1 $p |- ( ( x e. On /\ R We A /\ D =/= (/) ) -> ( F ` x ) e. D
      ) $=
        ( cv con0 wcel wwe c0 wne cfv wbr wn wral crab cuni wa cres tfr2 wceq
        copab fveq1i wfun cvv tfrlem7 visset resfunexg mp2an cima ssrab2
        eqsstri ssexi rabex uniex cab crn rneq df-ima syl6eqr raleqdv rabbidv
        3eqtr4g eleq2d anbi12d abbidv df-rab unieqd fvopab eqtri syl6eq eleq1d
        wss wereucl mp3an2 syl5bir 3impib ) AUEZUFUGZFJUHZIUIUJZWQMUKZIUGZWRXBE
        UEDUEZJULUMZEIUNZDIUOZUPZIUGZWSWTUQWRXAXGIWRXAMWQURZNUKZXGPQAGKMNSTUSXJ
        XIQUEXDEHUNZDHUOZUPZUTKQVAZUKXGXINXNUCVBKQXIXMXGMVCWQVDUGXIVDUGPQGKMNST
        VEAVFMWQVDVGVHXFXEDIIFRILUECUEJULZLMWQVIZUNZCFUOZFUBXQCFVJVKZVLZVMVNKUE
        ZXIUTZXLXFYBXCHUGZXKUQZDVOXCIUGZXEUQZDVOXLXFYBYDYFDYBYCYEXKXEYBHIXCYBXO
        LYAVPZUNZCFUOXRHIYBYHXQCFYBXOLYGXPYBYGXIVPXPYAXIVQMWQVRVSVTWAUAUBWBZWCY
        BXDEHIYIVTWDWEXKDHWFXEDIWFWBWGWHWIWJWKWSIFWLWTXHXSDEFIJXTWMWNWOWP $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype . $)
      ordtypelem2 $p |- ( ( x e. On /\ R We A /\ D =/= (/) )
              -> ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
        ( cv con0 wcel wwe c0 wne w3a cfv cima wbr wi cdm wss onss wfn wceq
        tfr1 fndm ax-mp syl6ssr wfun tfrlem7 funfvima2 mpan syl 3ad2ant1 wral
        ordtypelem1 breq2 ralbidv elrab2 simprbi breq1 rcla4cv 3syl syld ) AUEZ
        UFUGZFJUHZIUIUJZUKZBUEZWAUGZWFMULZMWAUMZUGZWHWAMULZJUNZWBWCWGWJUOZWDWBW
        AMUPZUQZWMWBWAUFWNWAURMUFUSWNUFUTPQGKMNSTVAUFMVBVCVDMVEWOWMPQGKMNSTVFWA
        WFMVGVHVIVJWEWKIUGZLUEZWKJUNZLWIVKZWJWLUOABCDEFGHIJKLMNOPQRSTUAUBUCUDVL
        WPWKFUGWSWQCUEZJUNZLWIVKWSCWKFIWTWKUTXAWRLWIWTWKWQJVMVNUBVOVPWRWLLWHWIW
        QWHWKJVQVRVSVT $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype . $)
      ordtypelem3 $p |- ( ( x e. On /\ R We A /\ D =/= (/) )
            -> ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
        ( cv con0 wcel wwe c0 wne w3a cfv wbr wceq wn ordtypelem2 wpo wi wor
        weso sopo syl 3ad2ant2 ordtypelem1 cima wral crab ssrab2 eqsstri sseli
        wa breq1 biimprcd poirr nsyli com12 syl11anc syld ) AUEZUFUGZFJUHZIUIUJ
        ZUKZBUEZVSUGWDMULZVSMULZJUMZWFWEUNZUOZABCDEFGHIJKLMNOPQRSTUAUBUCUDUPWCF
        JUQZWFFUGZWGWIURWAVTWJWBWAFJUSWJFJUTFJVAVBVCWCWFIUGWKABCDEFGHIJKLMNOPQR
        STUAUBUCUDVDIFWFILUECUEJUMLMVSVEVFZCFVGFUBWLCFVHVIVJVBWGWJWKVKZWIWGWHWF
        WFJUMZWMWHWNWGWFWEWFJVLVMFWFJVNVOVPVQVR $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype .  There is a smallest ordinal number whose image
         has no upper bounds in ` A ` . $)
      ordtypelem4 $p |- ( R We A
            -> E. x e. On ( D = (/) /\ A. y e. x H =/= (/) ) ) $=
        ( vr wwe c0 wceq con0 wrex wne cv wral wa crn cvv wcel wn pm3.24 wi
        wal wss cfv ax-17 hba1 hban eleq1 ordtypelem1 3expb wbr cima crab
        ssrab2 eqsstri sseli syl syl5cbi exp32 com12 a2d a4sd imp r19.23ad wfn
        wb tfr1 fvelrnb ax-mp syl5ib ssrdv ssex ex ccnv wfun ordtypelem3
        3com12 3exp imp4a 19.21adv alimdv r2al syl6ibr cres ssid tz7.48lem
        mpan wrel cdm tfrlem6 fndm eqimssi relssres mp2an cnveqi funeq sylib
        syl6 onprc funrnex df-rn eleq1i dfdm4 eqtr3i 3imtr4g mtoi jcad df-ne
        ralbii df-ral ralnex 3bitr3i syl5ibr mt3i imaeq2 raleqdv rabbidv
        3eqtr4g eqeq1d onminex anbi2i rexbii sylibr ) FJUFZIUGUHZAUIUJZUUDOUGUK
        ZBAULZUMZUNZAUIUJZUUCUUEMUOZUPUQZUULURZUNZUULUSUUCUUGUIUQZIUGUKZUTZAVAZ
        UUNUUEURZUUCUURUULUUMUUCUURUULUUCUURUNZUUKFVBUULUUTUEUUKFUUTUUGMVCZUEUL
        ZUHZAUIUJZUVBFUQZUVBUUKUQZUUTUVCUVEAUIUUCUURAUUCAVDUUQAVEVFUVEAVDUUCUUR
        UUOUVCUVEUTZUTZUUCUUQUVHAUUCUUOUUPUVGUUOUUCUUPUVGUTUUOUUCUUPUVGUVCUVAFU
        QZUVEUUOUUCUUPUNUNZUVAUVBFVGUVJUVAIUQZUVIUUOUUCUUPUVKABCDEFGHIJKLMNOPQR
        STUAUBUCUDVHVIIFUVAILULCULJVJZLMUUGVKZUMZCFVLZFUBUVNCFVMVNVOVPVQVRVSVTW
        AWBWCMUIWDZUVFUVDWEPQGKMNSTWFZAUIUVBMWGWHWIWJUUKFRWKVPWLUUCUURMWMZWNZUU
        MUUCUURUVABULZMVCUHURZBUUGUMAUIUMZUVSUUCUURUUOUVTUUGUQZUNUWAUTZBVAZAVAU
        WBUUCUUQUWEAUUCUUQUWDBUUCUUQUUOUWCUWAUUCUUOUUPUWCUWAUTZUUCUUOUUPUWFUUOU
        UCUUPUWFABCDEFGHIJKLMNOPQRSTUAUBUCUDWOWPWQVTWRWSWTUWAABUIUUGXAXBUWBMUIX
        CZWMZWNZUVSUIUIVBUWBUWIUIXDABUIMUVQXEXFUWHUVRUHUWIUVSWEUWGMMXGMXHZUIVBU
        WGMUHPQGKMNSTXIUWJUIUVPUWJUIUHUVQUIMXJWHZXKMUIXLXMXNUWHUVRXOWHXPXQUVSUU
        LUIUPUQZXRUVSUVRXHZUPUQZUVRUOZUPUQZUULUWLUWNUVSUWPUPUVRXSVSUUKUWMUPMXTY
        AUIUWOUPUWJUIUWOUWKMYBYCYAYDYEXQYFUUPAUIUMUUDURZAUIUMUURUUSUUPUWQAUIIUG
        YGYHUUPAUIYIUUDAUIYJYKYLYMUUEUUDOUGUHZURZBUUGUMZUNZAUIUJUUJUUDUWRABUUGU
        VTUHZIOUGUXBUVOUVLLMUVTVKZUMZCFVLIOUXBUVNUXDCFUXBUVLLUVMUXCUUGUVTMYNYOY
        PUBUDYQYRYSUUIUXAAUIUUHUWTUUDUUFUWSBUUGOUGYGYHYTUUAUUBVP $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype .  Establish injectivity of ` F ` when restricted
         to the ordinal number of ~ ordtypelem4 . $)
      ordtypelem5 $p |- ( ( ( R We A /\ x e. On ) /\ ( D = (/)
            /\ A. y e. x H =/= (/) ) ) -> Fun `' ( F |` x ) ) $=
        ( vz vr vm wwe cv con0 wcel wa c0 wceq wne wral wss cfv wn cres ccnv
        wfun onss ad2antlr wi csb visset ax-17 hbcsb1 hbne csbeq1a neeq1d
        rcla4 w3a wal ordtypelem3 gen2 cvv hb3an hbim hbal eleq1 3anbi13d
        eleq2 fveq2 eqeq1d notbid imbi12d albidv cla4gf ax-mp eqeq2d imbi2d
        a4v syl 3expia ancoms onelon sylan2 expr com3l wbr cima crab imaeq2
        raleqdv rabbidv 3eqtr4g cbvcsbv neeq1i syl5ib syld com13 imp4b adantrl
        r19.21aivv tfr1 tz7.48lem syl11anc ) FJUHZAUIZUJUKZULZIUMUNZOUMUOZBYAUP
        ZULZULZYAUJUQZUEUIZMURZUFUIZMURZUNZUSZUFYJUPUEYAUPMYAUTVAVBYBYIXTYGYAVC
        VDYHYOUEUFYAYJYCYFYJYAUKZYLYJUKZULYOVEYDYCYFYPYQYOYPYFYCYQYOVEZYPYFBYJO
        VFZUMUOZYCYRVEZYEYTBYJYABUGYSUMBUGYJOUEVGZUGUIZYJUKZBVHVIUUCUMUKZBVHVJB
        UIZYJUNOYSUMBYJOVKVLVMYPAYJIVFZUMUOZUUAYTYCYPUUHYRXTYBYPUUHYRVEZXTYJUJU
        KZUUIYBYPULUUJXTUUIUUJXTUUHYRYBXTIUMUOZVNZUUFYAUKZYAMURZUUFMURZUNZUSZVE
        ZVEZBVOZAVOZUUJXTUUHVNZYRVEZUUSABABCDEFGHIJKLMNOPQRSTUAUBUCUDVPVQUVAUVB
        UUFYJUKZYKUUOUNZUSZVEZVEZBVOZUVCYJVRUKZUVAUVIVEUUBUUTUVIAUGYJVRUUDAVHZU
        VHABUVBUVGAUUJXTUUHAUUJAVHXTAVHAUGUUGUMAUGYJIUUBUVKVIUUEAVHVJVSUVGAVHVT
        WAYAYJUNZUUSUVHBUVLUULUVBUURUVGUVLYBUUJUUKUUHXTYAYJUJWBUVLIUUGUMAYJIVKV
        LWCUVLUUMUVDUUQUVFYAYJUUFWDUVLUUPUVEUVLUUNYKUUOYAYJMWEWFWGWHWHWIWJWKUVH
        UVCBUFUUFYLUNZUVGYRUVBUVMUVDYQUVFYOUUFYLYJWBUVMUVEYNUVMUUOYMYKUUFYLMWEW
        LWGWHWMWNWOWKWPWQYAYJWRWSWTXAYSUUGUMUVJYSUUGUNUUBBAYJOIVRUUFYAUNZLUICUI
        JXBZLMUUFXCZUPZCFXDUVOLMYAXCZUPZCFXDOIUVNUVQUVSCFUVNUVOLUVPUVRUUFYAMXEX
        FXGUDUBXHXIWKXJXKXLXMXNXOXPUEUFYAMPQGKMNSTXQXRXS $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype .  If ` z ` is not in ` ( F " x ) ` , ` m ` is in
         ` ( F " x ) ` , and every element of ` ( F " x ) ` which is less than
         ` m ` is also less than ` z ` , ` m ` is also less than ` z ` . $)
      ordtypelem6 $p |- ( ( ( ( R We A /\ x e. On ) /\ ( ( F " x ) C_ A
              /\ A. y e. x H =/= (/) ) ) /\ ( ( z e. A /\ -. z e. ( F " x ) )
              /\ m e. ( F " x ) ) )
            -> ( A. n ( n R m -> ( n e. ( F " x ) -> n R z ) ) -> m R z ) ) $=
        ( vr wwe cv con0 wcel wa cima wss c0 wne wral wn wbr wi wal wceq eleq1
        anbi2d imaeq2 sseq1d raleq anbi12d eleq2d notbid imbi1d imbi2d albidv
        imbi12d a4v cfv wrex crab cuni cres onelon tfr2 syl adantll adantlr
        copab fveq1i wfun cvv tfrlem7 visset resfunexg mp2an rabex eqeltri
        uniex cab crn rneq df-ima syl6eqr raleqdv rabbidv 3eqtr4g abbidv
        df-rab unieqd fvopab eqtri syl6eq eqeq1d breq2 breq1 wsbc wreu simpll
        ad2antrr ssrab2 eqsstri a1i neeq1d rcla4cv adantl ad2antlr imp wereu
        syl111anc reuuni4 ax-17 hbra1 hbrab hbuni hbbr hbn hbim cla4gf ax-mp
        simprl simplll adantr wereucl ra4 com12 adantld eleq2i ralbidv elrab
        bitri syl5ib mpd imass2 sseld onelss simpllr sylan idd mpid imim2d ex
        com23 sylan2 r19.21aiv jca bitr2i sylib syl5com wo ioran simplrr
        syl6req cdm wfn tfr1 fndm syl6eleqr jctil simpr funfvima sylc eqeltrd
        syl5cbi mtod sylanbrc wor wb weso sseldd simplrl sotric syl12anc
        mpbird syld df-ral cbvsbc hbeq ralbid sbcie sylbid r19.23adv fvelima
        mpan syl5 impr mpg ) GKUIZUHUJZUKULZUMZPUXGUNZGUOZRUPUQZBUXGURZUMZUMZCU
        JZGULZUXPUXJULZUSZUMZNUJZUXJULZUMZUMZOUJZUYAKUTZUYEUXJULZUYEUXPKUTZVAZV
        AZOVBZUYAUXPKUTZVAZVAZUXFAUJZUKULZUMZPUYOUNZGUOZUXLBUYOURZUMZUMZUXQUXPU
        YRULZUSZUMZUYAUYRULZUMZUMZUYFUYEUYRULZUYHVAZVAZOVBZUYLVAZVAZUHUYNVUNUHA
        UXGUYOVCZUYDVUHUYMVUMVUOUXOVUBUYCVUGVUOUXIUYQUXNVUAVUOUXHUYPUXFUXGUYOUK
        VDVEVUOUXKUYSUXMUYTVUOUXJUYRGUXGUYOPVFZVGUXLBUXGUYOVHVIVIVUOUXTVUEUYBVU
        FVUOUXSVUDUXQVUOUXRVUCVUOUXJUYRUXPVUPVJVKVEVUOUXJUYRUYAVUPVJVIVIVUOUYKV
        ULUYLVUOUYJVUKOVUOUYIVUJUYFVUOUYGVUIUYHVUOUXJUYRUYEVUPVJVLVMVNVLVOVPUXO
        UXTUYBUYMUXOUXTUMZUYOPVQZUYAVCZAUXGVRZUYMUYBVUQVUSUYMAUXGVUQUYOUXGULZVU
        SUYMVAVUQVVAUMZVUSFUJZEUJZKUTZUSZFJURZEJVSZVTZUYAVCZUYMVVBVURVVIUYAVVBV
        URPUYOWAZQVQZVVIUXOVVAVURVVLVCZUXTUXIVVAVVMUXNUXHVVAVVMUXFUXHVVAUMZUYPV
        VMUXGUYOWBZSTAHLPQUBUCWCWDWEWFWFZVVLVVKTUJVVFFIURZEIVSZVTZVCLTWGZVQVVIV
        VKQVVTUFWHLTVVKVVSVVIPWIZUYOWJULVVKWJULSTHLPQUBUCWKZAWLPUYOWJWMWNVVHVVG
        EJJMUJZDUJZKUTZMUYRURZDGVSZWJUEVWFDGUAWOWPZWOWQZLUJZVVKVCZVVRVVHVWKVVDI
        ULZVVQUMZEWRVVDJULZVVGUMZEWRVVRVVHVWKVWMVWOEVWKVWLVWNVVQVVGVWKIJVVDVWKV
        WEMVWJWSZURZDGVSVWGIJVWKVWQVWFDGVWKVWEMVWPUYRVWKVWPVVKWSUYRVWJVVKWTPUYO
        XAXBXCXDUDUEXEZVJVWKVVFFIJVWRXCVIXFVVQEIXGVVGEJXGXEXHXIXJZXKXLVVJUYEVVI
        KUTZUYIVAZOVBZVVIUXPKUTZVAZUYMVVBVVJVXBUYKVXCUYLVVJVXAUYJOVVJVWTUYFUYIV
        VIUYAUYEKXMVLVNVVIUYAUXPKXNVOVVBVVGEVVIXOZVXDVVBVVGEJXPZVXEVVBUXFJGUOZJ
        UPUQZVXFUXOUXFUXTVVAUXFUXHUXNXQXRZVXGVVBJVWGGUEVWFDGXSXTZYAVUQVVAVXHUXN
        VVAVXHVAZUXIUXTUXMVXKUXKUXLVXHBUYOUXGBUJZUYOVCZRJUPVXMVWEMPVXLUNZURZDGV
        SVWGRJVXMVXOVWFDGVXMVWEMVXNUYRVXLUYOPVFXCXDUGUEXEYBYCZYDYEYFZEFGJKVWHYG
        YHVVGEJYIWDVVBVVCVVIKUTZUSZFJURZVXDVXEVVBVXBVXTVXCVVBVXBVXTVXCVAVVBVXBU
        MZVVCJULZVXSVAZFVBZVXCVXTVYAVYDUXPVVIKUTZUSZVXCVYDUXPJULZVYFVYAUXPWJULV
        YDVYGVYFVAZVACWLVYCVYHFNUXPWJUYAUXPULFYJZVYGVYFFVYGFYJVYEFFNUXPVVIKVYIU
        YAKULFYJFNVVHVVGFENJVVFFJYKUYAJULFYJYLYMZYNYOYPVVCUXPVCZVYBVYGVXSVYFVVC
        UXPJVDVYKVXRVYEVVCUXPVVIKXNVKVOYQYRVYAUXQVWCUXPKUTZMUYRURZUMZVYGVYAUXQV
        YMVUQUXQVVAVXBUXOUXQUXSYSXRVYAVYLMUYRVVBVWCVVIKUTZVWCUXJULZVYLVAZVAZVWC
        UYRULZVYLVAZVXBVVBVYRVYTVVBVYSVYRVYLVVBVYSVYRVYLVAVVBVYSUMZVYRVYOVYLWUA
        VVIJULZVYOWUAUXFVXGVXHWUBVUQUXFVVAVYSUXFUXHUXNUXTYTXRVXGWUAVXJYAVVBVXHV
        YSUXOVVAVXHUXTUXNVVAVXHUXIUXMVVAVXHUXKUXMVVAVXHVXPYFWEWEWFUUAEFGJKVWHUU
        BZYHWUAVVIGULZVYOMUYRURZUMZVYOWUBWUAWUEVYOWUDVYSWUEVYOVAVVBWUEVYSVYOVYO
        MUYRUUCUUDYDUUEWUBVVIVWGULWUFJVWGVVIUEUUFVWFWUEDVVIGVWDVVIVCZVWEVYOMUYR
        VWDVVIVWCKXMUUGUUHUUIUUJUUKWUAVYQVYLVYOWUAVYQVYPVYLUYOUXGUOZVYSVYPVVBWU
        HVYSVYPWUHUYRUXJVWCUYOUXGPUULUUMYFUXHVVAWUHVUQUXHVVAWUHUXGUYOUUNYFUXFUX
        HUXNUXTUUOUUPUUPWUAVYQUUQUURUUSUURUUTUVAYFVXAVYROMUYEVWCVCZVWTVYOUYIVYQ
        UYEVWCVVIKXNWUIUYGVYPUYHVYLUYEVWCUXJVDUYEVWCUXPKXNVOVOVPUVBUVCUVDVYGUXP
        VWGULVYNJVWGUXPUEUUFVWFVYMDUXPGVWDUXPVCVWEVYLMUYRVWDUXPVWCKXMUUGUUHUVEU
        VFUVGVYAVYFVXCVYAVYFUMZVXCVVIUXPVCZVYEUVHUSZWULWUKUSVYFWUJWUKVYEUVIWUJW
        UKUXRVVBUXSVXBVYFUXOUXQUXSVVAUVJXRVVBWUKUXRVAVXBVYFWUKVVIUXJULUXRVVBVVI
        UXPUXJVDVVBVVIVURUXJVVBVURVVLVVIVVPVWSUVKVVBVWAUYOPUVLZULZUMVVAVURUXJUL
        VVBWUNVWAUXOVVAWUNUXTUXIVVAWUNUXNUXHVVAWUNUXFVVNUYOUKWUMVVOPUKUVMWUMUKV
        CSTHLPQUBUCUVNUKPUVOYRUVPWEWFWFVWBUVQVUQVVAUVRUXGUYOPUVSUVTUWAUWBXRUWCV
        YAVYFUVRUWDWUJGKUWEZWUDUXQVXCWULUWFVVBWUOVXBVYFUXOWUOUXTVVAUXFWUOUXHUXN
        GKUWGXRXRXRWUJJGVVIVXGWUJVXJYAZWUJUXFVXGVXHWUBVVBUXFVXBVYFVXIXRWUPVVBVX
        HVXBVYFVXQXRWUCYHUWHVVBUXQVXBVYFUXOUXQUXSVVAUWIXRGVVIUXPKUWJUWKUWLUUTUW
        MVXSFJUWNUUJUUTUVAVXEVVCVWDKUTZUSZFJURZDVVIXOZVXTVVIWJULVXEWUTUWFVWIVVG
        WUSEDVVIWJVVGDYJWUSEYJVVDVWDVCZVVFWURFJWVAVVEWUQVVDVWDVVCKXMVKUUGUWOYRW
        USVXTDVVIVWIWUGWURVXSFJFNNVWDVVIUYAVWDULFYJVYJUWPWUGWUQVXRVWDVVIVVCKXMV
        KUWQUWRUUIUUJUUKUWBUWSUUTUWTVWAUYBVUTVWBAUYAUXGPUXAUXBUXCUXDUXE $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype . ` F ` maps some ordinal isomorphically onto
         ` A ` . $)
      ordtypelem7 $p |- ( R We A
            -> E. x e. On ( F |` x ) Isom _E , R ( x , A ) ) $=
        ( vz vs vn vm wwe c0 wceq wne cv wral wa con0 wrex cep cres wiso
        ordtypelem4 wcel wf1o wbr cfv wb df-iso wfn ccnv wfun crn w3a wss onss
        tfr1 fnssres mpan syl ad2antlr ordtypelem5 cima ax-17 hbra1 hban wi
        eleq1 ordtypelem1 3expb crab ssrab2 eqsstri sseli syl5bi onelon sylani
        com12 exp43 com3r imp a2d ra4 syl5 r19.23ad tfrlem7 fvelima ssrdv
        adantrl wn wex cuni simpll a1i simpl wal ordtypelem6 breq1 notbid
        elrab notbii iman bitr4i imbi2i albii syl5ib con3d expr imp3a
        r19.21aiv con2b df-ral ralbii ralnex bitri sylib wreu ad2antrr simprl
        sstrd hbrab1 hbne rabid ne0i sylbir ex r19.23ai adantl cvv visset
        funimaex ax-mp ssexi wereu syl111anc reurex rexnal syl5ibr mt3d jcad
        breq2 ralbidv syl6ibr syl6 19.23adv impr wereucl syl6eleqr n0i nss
        con4d com23 jcai eqss sylibr df-ima syl5eqr 3jca dff1o2 csb adantll
        ad2ant2r simplll hbcsb1 csbeq1a neeq1d rcla4 impcom ad2ant2lr csbieb
        imaeq2 raleqdv rabbidv syl5eq mpgbi ordtypelem2 3eqtr4g cbvcsbv neeq1i
        syl3an3b wo fveq2 ad2ant2rl ad2ant2l orim12d word eloni ordtri2
        con2bid syl11anc wor weso ad3antrrr sseldd hbss sseq1d chvar sotric
        syl12anc 3imtr3d impcon4bid epel fvres breqan12rd 3bitr4d r19.21adva
        r19.21ai sylanbrc exp31 reximdvai mpd ) FJUIZIUJUKZOUJULZBAUMZUNZUOZAUP
        UQUYMFURJMUYMUSZUTZAUPUQABCDEFGHIJKLMNOPQRSTUAUBUCUDVAUYJUYOUYQAUPUYJUY
        MUPVBZUYOUYQUYQUYMFUYPVCZBUMZUEUMZURVDZUYTUYPVEZVUAUYPVEZJVDZVFZUEUYMUN
        ZBUYMUNZUYJUYRUOZUYOUOZBUEUYMFURJUYPVGVUJUYPUYMVHZUYPVIVJZUYPVKZFUKZVLU
        YSVUJVUKVULVUNUYRVUKUYJUYOUYRUYMUPVMZVUKUYMVNMUPVHVUOVUKPQGKMNSTVOUPUYM
        MVPVQVRVSABCDEFGHIJKLMNOPQRSTUAUBUCUDVTVUJMUYMWAZFVUMVUJVUPFVMZFVUPVMZU
        OVUPFUKVUJVUQVURVUIUYNVUQUYKVUIUYNUOZUFVUPFVUSUYTMVEZUFUMZUKZBUYMUQZVVA
        FVBZVVAVUPVBZVUSVVBVVDBUYMVUIUYNBVUIBWBUYLBUYMWCWDZVVDBWBVUIUYNUYTUYMVB
        ZVVBVVDWEZWEZVUIVVGUYLWEVVIUYNVUIVVGUYLVVHUYJUYRVVGUYLVVHWEZWEUYRVVGUYJ
        VVJUYRVVGUYJUYLVVHVVBUYRVVGUOZUYJUYLUOZUOVVDVVBUYTUPVBZVVLVVDVVKVVBVUTF
        VBZVVDVVMVVLUOZVUTVVAFWFVVOVUTOVBZVVNVVMUYJUYLVVPBACDEFGHOJKLMNIPQRSTUA
        UDUCUBWGZWHOFVUTOLUMZCUMZJVDZLMUYTWAZUNZCFWIZFUDVWBCFWJWKZWLVRWMUYMUYTW
        NZWOWPWQWRWSWTUYLBUYMXAZXBWSXCMVJZVVEVVCPQGKMNSTXDZBVVAUYMMXEVQXBXFXGVU
        IUYOVUQVURWEVUIVUQUYOVURVUIVUQUYOVURWEVUIVUQUOZUYKUYNVURVWIUYNUYKVURVUI
        VUQUYNUYKVURWEVUIVUQUYNUOZUOZVURUYKVWKVUAFVBZVUAVUPVBXHZUOZUEXIZUYKXHZV
        URXHVWKVWOUGUMZUHUMZJVDZXHZUGVVTLVUPUNZCFWIZUNUHVXBWIXJZIVBZVWPVUIVWJVW
        OVXDVUIVWJVWOUOZUOZVXCVXBIVXFUYJVXBFVMZVXBUJULZVXCVXBVBUYJUYRVXEXKVXGVX
        FVXACFWJZXLVUIVWJVWOVXHVWKVWNVXHUEVWKVWNVUAVXBVBZVXHVWKVWNVWLVVRVUAJVDZ
        LVUPUNZUOVXJVWKVWNVWLVXLVWNVWLWEVWKVWLVWMXMXLVWKVWNVXLVWKVWNUOZVXLVWTUG
        VXKXHZLVUPWIZUNZUHVXOUQZVXMVWSVWQVXOVBZXHZWEZUGXNZXHZUHVXOUNZVXQXHZVXMV
        YBUHVXOVXMVWRVUPVBZVWRVUAJVDZXHZUOVYBVWRVXOVBVXMVYEVYGVYBVWKVWNVYEVYGVY
        BWEVWKVWNVYEUOUOZVYAVYFVYHVWSVWQVUPVBZVWQVUAJVDZWEZWEZUGXNVYFVYAABUECDE
        FGHIJKLUHUGMNOPQRSTUAUBUCUDXOVXTVYLUGVXSVYKVWSVXSVYIVYJXHZUOZXHVYKVXRVY
        NVXNVYMLVWQVUPVVRVWQUKVXKVYJVVRVWQVUAJXPXQXRXSVYIVYJXTYAYBYCYDYEYFYGVXN
        VYGLVWRVUPVVRVWRUKVXKVYFVVRVWRVUAJXPXQXRYDYHVYCVXPXHZUHVXOUNVYDVYBVYOUH
        VXOVYAVXPVYAVXRVWTWEZUGXNVXPVXTVYPUGVWSVXRYIYCVWTUGVXOYJYAXSYKVXPUHVXOY
        LYMYNVXMVXNLVUPUQZVXQVXLXHVXMVYQVXQVXMVYQUOZVXPUHVXOYOZVXQVYRUYJVXOFVMV
        XOUJULZVYSVWKUYJVWNVYQUYJUYRVWJXKYPVYRVXOVUPFVXOVUPVMVYRVXNLVUPWJZXLVWK
        VUQVWNVYQVUIVUQUYNYQYPYRVYQVYTVXMVXNVYTLVUPLUHVXOUJVXNLUHVUPYSVWRUJVBZL
        WBYTVVRVUPVBZVXNVYTWUCVXNUOVVRVXOVBVYTVXNLVUPUUAVXOVVRUUBUUCUUDUUEUUFUH
        UGFVXOJVXOVUPVWGVUPUUGVBVWHMUYMAUUHUUIUUJWUAUUKUULUUMVXPUHVXOUUNVRUUDVX
        KLVUPUUOUUPUUQUUDUURVXAVXLCVUAFVVSVUAUKVVTVXKLVUPVVSVUAVVRJUUSUUTXRUVAV
        XBVUAUUBUVBUVCUVDUHUGFVXBJVXBFRVXIUUKUVEUUMUBUVFYFIVXCUVGUVBUEFVUPUVHYD
        UVIYFUVJYGUUDUVJWSUVKVUPFUVLUVMMUYMUVNUVOUVPUYMFUYPUVQUVMVUIUYNVUHUYKVU
        SVUGBUYMVVFVUSVVGVUFUEUYMVUSVUAUYMVBZVVGVUFVUSWUDVVGUOZUOZUYTVUAVBZVUTV
        UAMVEZJVDZVUBVUEWUFWUGWUIWUFVUAUPVBZUYJBVUAOUVRZUJULZWUGWUIWEZVUIWUDWUJ
        UYNVVGUYRWUDWUJUYJUYMVUAWNUVSUVTZUYJUYRUYNWUEUWAZUYNWUDWULVUIVVGWUDUYNW
        ULUYLWULBVUAUYMBUHWUKUJBUHVUAOUEUUHZVWRVUAVBBWBUWBZWUBBWBYTUYTVUAUKZOWU
        KUJBVUAOUWCZUWDUWEUWFUWGZWUJUYJAVUAIUVRZUJULWUMWULUEBCDEFGHWVAJKLMNOPQR
        STUAUYMVUAUKZIVVTLMVUAWAZUNZCFWIZUKWEWVAWVEUKAAUHVUAIWVEWUPVWRWVEVBZAWB
        UWHWVBVXBWVEIWVBVXAWVDCFWVBVVTLVUPWVCUYMVUAMUWIUWJUWKUBUWLUWMZUCUDUWNWU
        KWVAUJVUAUUGVBWUKWVAUKWUPBAVUAOIUUGUYTUYMUKZVWCVXBOIWVHVWBVXACFWVHVVTLV
        WAVUPUYTUYMMUWIUWJUWKUDUBUWOUWPUUJUWQUWRUUMWUFWURVUAUYTVBZUWSZVUTWUHUKZ
        WUHVUTJVDZUWSZWUGXHZWUIXHZWUFWURWVKWVIWVLWURWVKWEWUFUYTVUAMUWTXLWUFVVMU
        YJUYLWVIWVLWEVUIVVGVVMUYNWUDUYRVVGVVMUYJVWEUVSUXAZWUOUYNVVGUYLVUIWUDUYN
        VVGUYLVWFWSUXBZBUECDEFGHOJKLMNWVAPQRSTUAUDUCWVGUWNUUMUXCWUFUYTUXDZVUAUX
        DZWVJWVNVFWUFVVMWVRWVPUYTUXEVRWUFWUJWVSWUNVUAUXEVRWVRWVSUOWUGWVJUYTVUAU
        XFUXGUXHWUFFJUXIZVVNWUHFVBZWVMWVOVFUYJWVTUYRUYNWUEFJUXJUXKWUFOFVUTOFVMZ
        WUFVWDXLWUFVVMUYJUYLVVPWVPWUOWVQVVQUUMUXLWUFWUKFWUHWUKFVMZWUFWWBWWCBUEB
        UHWUKFWUQVWRFVBBWBUXMWUROWUKFWUSUXNVWDUXOXLWUFWUJUYJWULWUHWUKVBWUNWUOWU
        TUEACDEFGHWUKJKLMNIPQRSTUAWUROWVEUKWEWUKWVEUKBBUHVUAOWVEWUPWVFBWBUWHWUR
        VWCWVEOWURVWBWVDCFWURVVTLVWAWVCUYTVUAMUWIUWJUWKUDUWLUWMUCUBWGUUMUXLWVTV
        VNWWAUOUOWUIWVMFVUTWUHJUXPUXGUXQUXRUXSVUBWUGVFWUFBUEUXTXLWUEVUEWUIVFVUS
        VVGWUDVUCVUTVUDWUHJUYTUYMMUYAVUAUYMMUYAUYBUUFUYCYFUYDUYEXGUYFUYGUYHUYI
        $.
        $( [17-Oct-2009] $)
    $}

    $( For any well-ordered set, there is an isomorphic ordinal number called
       its order type.  (Contributed by Jeff Hankins, 17-Oct-2009.) $)
    ordtype $p |- ( ( A e. B /\ R We A )
           -> E! x e. On E. f f Isom _E , R ( x , A ) ) $=
      ( vy va vg vr vs vo vm vn vk vz vq vw vv vu vh vb vp vj vc wcel wwe wa
      cv cep wiso wex con0 wrex wceq wi wral wreu weeq2 isoeq5 exbidv rexbidv
      imbi12d wfn cfv cres wbr wn crn crab cuni copab cab cima visset rdglem1
      eqid wb breq1 cbvralv a1i rabbiia breq2 ralbidv cbvrabv eqtri notbid
      unieqi eqeq2i opabbii rneq raleqdv rabbidv eleq2d anbi12d abbidv df-rab
      3eqtr4g unieqd eqeq2d eqeq1 sylan9bb cbvopabv ordtypelem7 wfun cvv
      tfrlem7 resfunexg mp2an isoeq1 cla4ev reximi syl vtoclg imp hbe1 ax-17
      hbim ccnv ccom ordiso cnvex coex syl5bir isotr syl5com ex isocnv syl5
      19.23adv cbvexv syl5ib 19.23ai com12 rgen2a jctir isoeq4 reu4 sylibr ) BC
      UEZBDUFZUGZAUHZBUIDEUHZUJZEUKZAULUMZUUEFUHZBUIDUUCUJZEUKZUGZUUBUUGUNZUOZF
      ULUPAULUPZUGUUEAULUQUUAUUFUUMYSYTUUFGUHZDUFZUUBUUNUIDUUCUJZEUKZAULUMZUOYT
      UUFUOGBCUUNBUNZUUOYTUURUUFUUNBDURUUSUUQUUEAULUUSUUPUUDEUUBUUNBUIDUUCUSUTV
      AVBUUOUUBUUNUIDHUHZIUHZVCJUHZUUTVDUUTUVBVEKUHZLUHZMUHZDVFZVGZLNUHZOUHZDVF
      ZNPUHZVHZUPZOUUNVIZUPZMUVNVIZVJZUNZPKVKZVDUNJUVAUPUGIULUMHVLZVJZUUBVEZUJZ
      AULUMUURAFQRSUUNUVTUVJNTUHZVHZUPZOUUNVIZUAUHZUBUHZDVFZUAUWAUUBVMZUPZUBUUN
      VIZDTUCUWAUVSUWJUAUWAUUGVMZUPZUBUUNVIZUAUDGVNIJUAUDHTUVSVOZUWAVPZUWGUCUHZ
      UVIDVFZUCUWEUPZOUUNVIUWSQUHZDVFZUCUWEUPZQUUNVIUWFUXAOUUNUWFUXAVQUVIUUNUEU
      VJUWTNUCUWEUVHUWSUVIDVRVSVTWAUXAUXDOQUUNUVIUXBUNUWTUXCUCUWEUVIUXBUWSDWBWC
      WDWEUWMUWSUWIDVFZUCUWKUPZUBUUNVIUXCUCUWKUPZQUUNVIUWLUXFUBUUNUWLUXFVQUWIUU
      NUEZUWJUXEUAUCUWKUWHUWSUWIDVRZVSVTWAUXFUXGUBQUUNUWIUXBUNZUXEUXCUCUWKUWIUX
      BUWSDWBZWCWDWEUVSUVCSUHZRUHZDVFZVGZSUVNUPZRUVNVIZVJZUNZPKVKUDUHZUXOSUWGUP
      ZRUWGVIZVJZUNZTUDVKUVRUXSPKUVQUXRUVCUVPUXQUVPUXLUVEDVFZVGZSUVNUPZMUVNVIUX
      QUVOUYGMUVNUVOUYGVQUVEUVNUEUVGUYFLSUVNUVDUXLUNUVFUYEUVDUXLUVEDVRWFVSVTWAU
      YGUXPMRUVNUVEUXMUNZUYFUXOSUVNUYHUYEUXNUVEUXMUXLDWBWFWCWDWEWGWHWIUXSUYDPKT
      UDUVKUWDUNZUXSUVCUYCUNUVCUXTUNUYDUYIUXRUYCUVCUYIUXQUYBUYIUXMUVNUEZUXPUGZR
      VLUXMUWGUEZUYAUGZRVLUXQUYBUYIUYKUYMRUYIUYJUYLUXPUYAUYIUVNUWGUXMUYIUVMUWFO
      UUNUYIUVJNUVLUWEUVKUWDWJWKWLZWMUYIUXOSUVNUWGUYNWKWNWOUXPRUVNWPUYARUWGWPWQ
      WRWSUVCUXTUYCWTXAXBWEUWPUXEUCUWNUPZUBUUNVIUXCUCUWNUPZQUUNVIUWOUYOUBUUNUWO
      UYOVQUXHUWJUXEUAUCUWNUXIVSVTWAUYOUYPUBQUUNUXJUXEUXCUCUWNUXKWCWDWEXCUWCUUQ
      AULUUPUWCEUWBUWAXDUUBXEUEUWBXEUEUAUDUVTTUWAUVSUWQUWRXFAVNUWAUUBXEXGXHUUBU
      UNUIDUWBUUCXIXJXKXLXMXNUULAFULUUJUUBULUEUUGULUEUGZUUKUUEUUIUYQUUKUOZUUDUU
      IUYRUOEUUIUYREUUHEXOUYREXPXQUUDUUGBUIDUUTUJZHUKUYRUUIUUDUYSUYRHUUDBUUGDUI
      UUTXRZUJZUYRUYSUUDVUAUYRUYQUUBUUGUIUIUYTUUCXSZUJZUUKUUDVUAUGUYQUUKUUBUUGU
      IUIUWDUJZTUKVUCUUBUUGTXTVUDVUCTVUBUYTUUCUUTHVNYAEVNYBUUBUUGUIUIVUBUWDXIXJ
      YCUUBBUUGUIDUIUYTUUCYDYEYFUUGBUIDUUTYGYHYIUUHUYSEHUUGBUIDUUTUUCXIYJYKYLXN
      YMYNYOUUEUUIAFULUUKUUDUUHEUUBBUUGUIDUUCYPUTYQYR $.
      $( [17-Oct-2009] $)
  $}

  ${
    $d a b c d f g k m n o p r s t w x y z A $.
    $d a b c d f g k m n o p r s t w y z B $.
    $d a b c d f g k m n o p r s t w y z H $.
    hartog.1 $e |- H = { x e. On | x ~<_ A } $.
    ${
      $d r s R $.
      hartoglem.2 $e |- R = { <. a , b >. | E. c e. t E. d e. t
         ( a = ( g ` c ) /\ b = ( g ` d ) /\ c e. d ) } $.
      $( Lemma for ~ hartog . $)
      hartoglem $p |- ( ( ( A e. B /\ g : t -1-1-> A ) /\ t e. On )
            -> g Isom _E , R ( t , ( g " t ) ) ) $=
        ( vr vs cv cima cep wiso wf1o wbr cfv wb wral wcel wf1 wa con0 df-iso
        wfo df-f1o wf ccnv wfun df-f1 crn wfn f1f ffn dffn4 fof sylbi 3syl
        wceq fnima feq3 syl mpbird simprbi sylanbrc df-fo eqcomd ad2antlr w3a
        wrex simprll simprlr eqidd simprr fveq2 eqeq2d eleq1 3anbi13d eleq2
        3anbi23d rcla42ev syl113anc expr simprr3 simprr1 simplr ad2antrr
        simplrl f1fveq syl12anc mpbid simprr2 simplrr eleq12d exp32 r19.23advv
        impbid epel fvex eqeq1 3anbi1d 2rexbidv 3anbi2d brab 3bitr4g ex
        r19.21aivv ) BPZFPZXMQZREXNSXMXOXNTZNPZOPZRUAZXQXNUBZXRXNUBZEUAZUCZOXMU
        DNXMUDCDUEZXMCXNUFZUGXMUHUEZUGZNOXMXOREXNUIYEXPYDYFXPXMXOXNUFZXMXOXNUJZ
        YEXMXOXNUKYHXMXOXNULZXNUMUNZYEXMXOXNUOYEYJXMXNUPZXNULZYEXMCXNULZXNXMUQZ
        YMXMCXNURZXMCXNUSZYOXMYLXNUJYMXMXNUTXMYLXNVAVBVCYEXOYLVDZYJYMUCYEYNYOYR
        YPYQXMXNVEVCZXOYLXMXNVFVGVHYEYNYKXMCXNUOVIVJYIYOYLXOVDYEXMXOXNVKYEYNYOY
        PYQVGYEXOYLYSVLVJVJVMYGYCNOXMXMYGXQXMUEZXRXMUEZUGZYCYGUUBUGZXQXRUEZXTJP
        ZXNUBZVDZYAKPZXNUBZVDZUUEUUHUEZVNZKXMVOJXMVOZXSYBUUCUUDUUMYGUUBUUDUUMYG
        UUBUUDUGUGZYTUUAXTXTVDZYAYAVDZUUDUUMYGYTUUAUUDVPYGYTUUAUUDVQUUNXTVRUUNY
        AVRYGUUBUUDVSUULUUOUUPUUDVNUUOUUJXQUUHUEZVNJKXQXRXMXMUUEXQVDZUUGUUOUUKU
        UQUUJUURUUFXTXTUUEXQXNVTWAUUEXQUUHWBWCUUHXRVDZUUJUUPUUQUUDUUOUUSUUIYAYA
        UUHXRXNVTWAUUHXRXQWDWEWFWGWHUUCUULUUDJKXMXMUUCUUEXMUEZUUHXMUEZUGZUULUUD
        UUCUVBUULUGZUGZUUDUUKUUGUUJUUKUVBUUCWIUVDXQUUEXRUUHUVDUUGXQUUEVDZUUGUUJ
        UUKUVBUUCWJUVDYEYTUUTUUGUVEUCYGYEUUBUVCYDYEYFWKWLZYGYTUUAUVCWMUUCUUTUVA
        UULVPXMCXQUUEXNWNWOWPUVDUUJXRUUHVDZUUGUUJUUKUVBUUCWQUVDYEUUAUVAUUJUVGUC
        UVFYGYTUUAUVCWRUUCUUTUVAUULVQXMCXRUUHXNWNWOWPWSVHWTXAXBNOXCHPZUUFVDZIPZ
        UUIVDZUUKVNZKXMVOJXMVOUUGUVKUUKVNZKXMVOJXMVOUUMHIXTYAEXQXNXDXRXNXDUVHXT
        VDZUVLUVMJKXMXMUVNUVIUUGUVKUUKUVHXTUUFXEXFXGUVJYAVDZUVMUULJKXMXMUVOUVKU
        UJUUGUUKUVJYAUUIXEXHXGMXIXJXKXLVJ $.
        $( [22-Oct-2009] $)
    $}

    $( Associate every set with an ordinal number known as its Hartog number.
       Apparently, this theorem has some sort of topological significance.  I
       guess you would have to ask someone like Jeff Madsen or Dr.  James
       Conant.  Notice the similarity of this theorem to ~ ondomon , but unlike
       that theorem, this one does not require the Axiom of Choice ~ ax-ac .
       (Contributed by Jeff Hankins, 22-Oct-2009.) $)
    hartog $p |- ( A e. B -> H e. On ) $=
      ( vy vz vw vo vf vt vp vg va vc vb vd vm wcel con0 word wtr wss cv cdom
      wbr crab wa wi wal breq1 elrab onelon ancoms adantrr domtr onelss impcom
      cvv visset ssdomg ax-mp syl sylan anasss sylanbrc sylan2b gen2 wceq wb
      treq dftr2 bitri mpbir ssrab2 eqsstri ordon trssord mp3an cxp wwe w3a
      cep wiso wex cuni copab2 crn cop wf1 brdomg cima cfv wrex copab imassrn
      a1i wf f1f frn sstrd ad2antlr simprl simprr1 adantl ad2antrr ffvelrn
      syl11anc eqeltrd simprr2 adantll ad2ant2rl opelxpi exp32 r19.23advv imp
      adantrl ex 19.23advv elopab syl5ib ssrdv eloni ordwe eqid hartoglem
      isowe mpbid 3jca adantr isoeq1 cla4ev wreu simplr imaexg simp3 ordtype
      isoeq4 exbidv reuuni2 eqcomd jcai cab abrexex abrexex2 ssab eqeq1 opeq12
      3adant3 syl5bir reximdv 19.23aivv sylbi mpgbir ssexi sseq1 weeq2
      3anbi13d isoeq5 rabbidv unieqd eqeq2d anbi12d weeq1 3anbi23d isoeq3
      anbi2d eloprabg sylibr opex opeq1 eleq1d exp31 19.23adv sylbid com23
      imp3a elrn2 3imtr4g cdm weu wfn mpan 3ad2ant3 reucl elisset 3syl eueq
      sylib 19.21aivv fnoprabg fndm cpw simpl elpw syl2anbr ssriv pwexg xpexg
      anidms ssexg wfun moeq moani funoprab funrnex mpi syl5eqel elong mpbiri
      ) BCSZDTSZDUAZDUBZDTUCTUAUXMUXNFUDZGUDZSZUXPAUDZBUEUFZATUGZSZUHUXOUXTSZUI
      ZGUJFUJZUYCFGUXQUXPTSZUXPBUEUFZUHZUYBUYAUYBUXOTSZUXOBUEUFZUXQUYGUHUXSUYIA
      UXOTUXRUXOBUEUKULUXQUYEUYHUYFUYEUXQUYHUXPUXOUMUNUOUXQUYEUYFUYIUXOUXPUEUFZ
      UYFUYIUXQUYEUHZUXOUXPBUPUYKUXOUXPUCZUYJUYEUXQUYLUXPUXOUQURUXOUSSZUYLUYJUI
      FUTZUXOUXPUSVAVBVCVDVEVFUXSUYFAUXPTUXRUXPBUEUKULVGVHUXNUXTUBZUYDDUXTVIUXN
      UYOVJEDUXTVKVBFGUXTVLVMVNDUXTTEUXSATVOVPVQDTVRVSUXKDUSSUXLUXMVJUXKUXTUSDU
      XKUXTUXOBUCZHUDZBBVTZUCZUXOUYQWAZWBZUXPIUDZUXOWCUYQJUDZWDZJWEZITUGZWFZVIZ
      UHZFHGWGZWHZUCVUKUSSZUXTUSSUXKKUXTVUKUXKKUDZTSZVUMBUEUFZUHLUDZVUMWIZVUJSZ
      LWEZVUMUXTSVUMVUKSUXKVUNVUOVUSUXKVUOVUNVUSUXKVUOVUMBMUDZWJZMWEVUNVUSUIZVU
      MBCMWKUXKVVAVVBMUXKVVAVUNVUSUXKVVAUHZVUNUHZVUTVUMWLZNUDZOUDZVUTWMZVIZPUDZ
      QUDZVUTWMZVIZVVGVVKSZWBZQVUMWNZOVUMWNZNPWOZWIZVUMWIZVUJSZVUSVVDVVEBUCZVVR
      UYRUCZVVEVVRWAZWBZVUMVUBVVEWCVVRVUCWDZJWEZITUGZWFZVIZUHZVWAVVDVWEVWJVVDVW
      BVWCVWDVVAVWBUXKVUNVVAVVEVUTWHZBVVEVWLUCVVAVUTVUMWPWQVVAVUMBVUTWRZVWLBUCV
      UMBVUTWSZVUMBVUTWTVCXAXBVVDRVVRUYRVVDRUDZVVFVVJWIZVIZVVQUHZPWENWEZVWOUYRS
      ZVWOVVRSZVVDVWRVWTNPVVDVWRVWTVVDVWRUHVWOVWPUYRVVDVWQVVQXCVVDVVQVWPUYRSZVW
      QVVDVVQVXBVVDVVOVXBOQVUMVUMVVDVVGVUMSZVVKVUMSZUHZVVOVXBVVDVXEVVOUHUHZVVFB
      SVVJBSVXBVXFVVFVVHBVVIVVMVVNVXEVVDXDVVDVXEVVHBSZVVOVVDVXEUHVWMVXCVXGVVCVW
      MVUNVXEVVAVWMUXKVWNXEXFVVDVXCVXDXCVUMBVVGVUTXGXHUOXIVXFVVJVVLBVVIVVMVVNVX
      EVVDXJVVDVXEVVLBSZVVOVVCVXDVXHVUNVXCVVAVXDVXHUXKVWMVXDVXHVVAVUMBVVKVUTXGV
      WNVDXKXLUOXIVVFVVJBBXMXHXNXOXPXQXIXRXSVVQNPVWOXTZYAYBVVDVUMWCWAZVWDVUNVXJ
      VVCVUNVUMUAVXJVUMYCVUMYDVCXEVVDVUMVVEWCVVRVUTWDZVXJVWDVJAKBCVVRMDNPOQEVVR
      YEYFZVUMVVEWCVVRVUTYGVCYHYIVVDVWEVWJVVDVWEUHZVWIVUMVXMVUMVVEWCVVRVUCWDZJW
      EZVWIVUMVIZVXMVXKVXOVVDVXKVWEVXLYJVXNVXKJVUTMUTZVUMVVEWCVVRVUTVUCYKYLVCVX
      MVUNVWGITYMZVXOVXPVJVVCVUNVWEYNVXMVVEUSSZVWDVXRVXSVXMVUTUSSVXSVXQVUTVUMUS
      YOVBZWQVWEVWDVVDVWBVWCVWDYPXEIVVEUSVVRJYQXHVWGVXOITVUMVUBVUMVIVWFVXNJVUBV
      VEVUMWCVVRVUCYRYSYTXHYHUUAXRUUBVXSVVRUSSVUMUSSVWAVWKVJVXTVVRVWOVVHVVLWIZV
      IZQVUMWNZOVUMWNZRUUCZVYCORVUMKUTZQRVUMVYAVYFUUDUUEVVRVYEUCVXAVYDUIRVYDRVV
      RUUFVXAVWSVYDVXIVWRVYDNPVWQVVQVYDVWQVVPVYCOVUMVWQVVOVYBQVUMVWQVYBVWPVYAVI
      ZVVOVWOVWPVYAUUGVVIVVMVYGVVNVVFVVJVVHVVLUUHUUIUUJUUKUUKXPUULUUMUUNUUOVYFV
      UIVWBUYSVVEUYQWAZWBZUXPVUBVVEWCUYQVUCWDZJWEZITUGZWFZVIZUHVWEUXPVWIVIZUHVW
      KFHGVVEVVRVUMUSUSUSUXOVVEVIZVUAVYIVUHVYNVYPUYPVWBUYTVYHUYSUXOVVEBUUPUXOVV
      EUYQUUQUURVYPVUGVYMUXPVYPVUFVYLVYPVUEVYKITVYPVUDVYJJVUBUXOVVEWCUYQVUCUUSY
      SUUTUVAUVBUVCUYQVVRVIZVYIVWEVYNVYOVYQUYSVWCVYHVWDVWBUYQVVRUYRUUPVVEUYQVVR
      UVDUVEVYQVYMVWIUXPVYQVYLVWHVYQVYKVWGITVYQVYJVWFJVUBVVEWCUYQVVRVUCUVFYSUUT
      UVAUVBUVCUXPVUMVIVYOVWJVWEUXPVUMVWIUUGUVGUVHVSUVIVURVWALVVSVVEVVRUVJVUPVV
      SVIVUQVVTVUJVUPVVSVUMUVKUVLYLVCUVMUVNUVOUVPUVQUXSVUOAVUMTUXRVUMBUEUKULLVU
      MVUJVYFUVRUVSYBUXKVUJUVTZUSSZVULUXKVYRVUAFHWOZUSUXKVUAVUHGUWAZUIZHUJFUJVU
      JVYTUWBVYRVYTVIUXKWUBFHUXKVUAWUAUXKVUAUHZVUGUSSZWUAWUCVUEITYMZVUGTSWUDVUA
      WUEUXKUYTUYPWUEUYSUYMUYTWUEUYNIUXOUSUYQJYQUWCUWDXEVUEITUWEVUGTUWFUWGGVUGU
      WHUWIXRUWJVUAVUHFHGUWKVYTVUJUWLUWGUXKVYTBUWMZUYRUWMZVTZUCZWUHUSSZVYTUSSWU
      IUXKKVYTWUHVUMVYTSVUMUXOUYQWIZVIZVUAUHZHWEFWEVUMWUHSZVUAFHVUMXTWUMWUNFHWU
      MVUMWUKWUHWULVUAUWNVUAWUKWUHSZWULUYPUYSWUOUYTUXOWUFSUYQWUGSWUOUYPUYSUXOUY
      QWUFWUGXMUXOBUYNUWOUYQUYRHUTUWOUWPUUIXEXIUULUUMUWQWQUXKWUFUSSWUGUSSZWUJBC
      UWRUXKUYRUSSZWUPUXKWUQBBCCUWSUWTUYRUSUWRVCWUFWUGUSUSUWSXHVYTWUHUSUXAXHXIV
      YSVUJUXBVULVUIFHGVUHVUAGGVUGUXCUXDUXEUSVUJUXFUXGVCUXTVUKUSUXAXHEUXHDUSUXI
      VCUXJ $.
      $( [27-Aug-2011] $) $( [22-Oct-2009] $)
  $}

  ${
    $d x y z A $.
    $( Any ordinal number is strictly dominated by some other ordinal number.
       (Contributed by Jeff Hankins, 22-Oct-2009.) $)
    onsdom $p |- ( A e. On -> E. x e. On A ~< x ) $=
      ( vy vz con0 wcel cv cdom wbr crab csdm wrex eqid hartog cen wn brsdom
      wss wa onelon ex onelss cvv wi visset ssdomg ax-mp syl6 jcad breq1 elrab
      syl6ibr ssrdv mpd word eloni ordirr 3syl a1d ancrd cbvrabv eleq2i bitr3i
      syl6ib mtod ensymg syl endom sylanbrc breq2 rcla4ev syl11anc ) BEFZCGZBHI
      ZCEJZEFZBVPKIZBAGZKIZAELCBEVPVPMNZVRBVPHIZBVPOIZPVMBVPQVMBVPRWBVMDBVPVMDG
      ZBFZWDEFZWDBHIZSWDVPFVMWEWFWGVMWEWFBWDTUAVMWEWDBRZWGBWDUBWDUCFWHWGUDDUEWD
      BUCUFUGUHUIVOWGCWDEVNWDBHUJUKULUMBVPEUFUNVMWCVPBHIZVMWIVPVPFZVMVQVPUOWJPW
      AVPUPVPUQURVMWIVQWISZWJVMWIVQVMVQWIWAUSUTWKVPWGDEJZFWJWGWIDVPEWDVPBHUJUKW
      LVPVPWGVODCEWDVNBHUJVAVBVCVDVEVMWCVPBOIZWIVMVQWCWMUDWABVPEVFVGVPBVHUHVEVI
      VTVRAVPEVSVPBKVJVKVL $.
      $( [22-Oct-2009] $)
  $}

