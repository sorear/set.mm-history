$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Undefined values and restricted iota (description binder)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Undef $.
  $c iota_ $.

  $( Extend class notation with undefined value function. $)
  cund $a class Undef $.

  $( Extend class notation with restricted description binder. $)
  crio $a class ( iota_ x e. A ph ) $.

  $( Def. 8.3.36 p. 181. $)
  $( Define the undefined value function, whose value at set ` s ` is
     guaranteed not to be a member of ` s ` (see ~ pwuninel ). $)
  df-undef $a |- Undef = ( s e. _V |-> ~P U. s ) $.

  ${
    $d s S $.
    undefval.1 $e |- S e. _V $.
    $( Value of the undefined value function.  Normally we will not reference
       the explicit value but will use ~ undefnel instead. $)
    undefval $p |- ( Undef ` S ) = ~P U. S $=
      ( vs cvv wcel cund cfv cuni cpw wceq cv unieq pweq syl df-undef uniex
      pwex fvmpt ax-mp ) ADEAFGAHZIZJBCACKZHZIZUADFUBAJUCTJUDUAJUBALUCTMNCOTABP
      QRS $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel2 $p |- -. ( Undef ` S ) e. S $=
      ( cund cfv wcel cuni cpw pwuninel undefval eleq1i mtbir ) ACDZAEAFGZAEAHL
      MAABIJK $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel $p |- ( Undef ` S ) e/ S $=
      ( cund cfv wnel wcel wn undefnel2 df-nel mpbir ) ACDZAEKAFGABHKAIJ $.
      $( [15-Sep-2011] $)
  $}

  ${
    $( Define restricted description binder.  In case it doesn't exist, we
       return a set which is not a member of the domain of discourse ` A ` . $)
    df-riota $a |- ( iota_ x e. A ph ) =
       if ( E! x e. A ph , ( iota x ( x e. A /\ ph ) ) , ( Undef ` A ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqdv.1 $e |- ( ph -> A = B ) $.
    $( Formula-building deduction rule for iota. $)
    riotaeqdv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ps ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio wceq wb reueq1 syl eleq2d anbi1d
      iotabidv fveq2d ifbieq12d df-riota 3eqtr4g ) ABCDGZCHZDIZBJZCKZDLMZNBCEGZ
      UHEIZBJZCKZELMZNBCDOBCEOAUGUMUKULUPUQADEPUGUMQFBCDERSAUJUOCAUIUNBADEUHFTU
      AUBADELFUCUDBCDUEBCEUEUF $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x ph $.
    riotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for restricted iota. $)
    riotabidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio reubidv anbi2d iotabidv eqidd
      ifbieq12d df-riota 3eqtr4g ) ABDEGZDHEIZBJZDKZELMZNCDEGZUDCJZDKZUGNBDEOCD
      EOAUCUHUFUGUJUGABCDEFPAUEUIDABCUDFQRAUGSTBDEUACDEUAUB $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqbidv.1 $e |- ( ph -> A = B ) $.
    riotaeqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    riotaeqbidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ch ) ) $=
      ( crio riotabidv riotaeqdv eqtrd ) ABDEICDEICDFIABCDEHJACDEFGKL $.
      $( [15-Sep-2011] $)
  $}

  $( Restricted iota is a set. $)
  riotaex $p |- ( iota_ x e. A ps ) e. _V $=
    ( crio wreu cv wcel wa cio cund cfv cif cvv df-riota iotaex fvex ifex
    eqeltri ) ABCDABCEZBFCGAHZBIZCJKZLMABCNSUAUBTBOCJPQR $.
    $( [15-Sep-2011] $)

  $( An iota restricted to the universe is unrestricted. $)
  riotav $p |- ( iota_ x e. _V ph ) = ( iota x ph ) $=
    ( cvv crio wreu cv wcel wa cio cund cfv cif df-riota wceq iftrue wb iotabi
    visset biantrur mpg syl6reqr wn c0 weu reuv notbii iotanul sylbi vprc
    fvprc ax-mp syl6eqr iffalse eqtr4d pm2.61i eqtr4i ) ABCDABCEZBFCGZAHZBIZCJK
    ZLZABIZABCMUQVCVBNUQVBUTVCUQUTVAOAUSPVCUTNBAUSBQURABRSTUAUQUBZVCVAVBVDVCUCV
    AVDABUDZUBVCUCNUQVEABUEUFABUGUHCCGUBVAUCNUICJUJUKULUQUTVAUMUNUOUP $.
    $( [18-Sep-2011] $)

  $( Restricted iota in terms of iota. $)
  riotaiota $p |- ( E! x e. A ph
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( wreu crio cv wcel wa cio cund cfv cif df-riota iftrue syl5eq ) ABCDZABCEP
    BFCGAHBIZCJKZLQABCMPQRNO $.
    $( [15-Sep-2011] $)

  $( For proper classes, restricted and unrestricted iota are the same. $)
  riotaprc $p |- ( -. A e. _V
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( cvv wcel wn wreu crio cv wa cio wceq cund cfv c0 fvprc adantr cif
    df-riota iffalse adantl syl5eq weu df-reu notbii iotanul sylbi 3eqtr4d ex
    riotaiota pm2.61d2 ) CDEFZABCGZABCHZBICEAJZBKZLZULUMFZUQULURJZCMNZOUNUPULUT
    OLURCMPQUSUNUMUPUTRZUTABCSURVAUTLULUMUPUTTUAUBURUPOLZULURUOBUCZFVBUMVCABCUD
    UEUOBUFUGUAUHUIABCUJUK $.
    $( [15-Sep-2011] $)

  $( Restricted iota in terms of class union. $)
  riotauni $p |- ( E! x e. A ph
                    -> ( iota_ x e. A ph ) = U. { x e. A | ph } ) $=
    ( wreu crio cv wcel wa cio crab cuni riotaiota cab weu wceq df-reu iotaequ
    sylbi df-rab unieqi syl6eqr eqtrd ) ABCDZABCEBFCGAHZBIZABCJZKZABCLUCUEUDBMZ
    KZUGUCUDBNUEUIOABCPUDBQRUFUHABCSTUAUB $.
    $( [11-Oct-2011] $)

  ${
    $d x y A $.  $d y ph $.
    $( The abstraction variable in a restricted iota descriptor isn't free. $)
    hbriota1 $p |- ( y e. ( iota_ x e. A ph )
                       -> A. x y e. ( iota_ x e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hbreu1 hbiota1 ax-17
      hbfv hbif hbxfreq ) BCABDEABDFZBGDHAIZBJZDKLZMABDNUABCUCUDABDOUBBCPBCDKCG
      ZKHBQUEDHBQRST $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.
    hbriota.1 $e |- ( ph -> A. x ph ) $.
    hbriota.2 $e |- ( z e. A -> A. x z e. A ) $.
    $( A variable not free in a wff remains so in a restricted iota
       descriptor. $)
    hbriota $p |- ( z e. ( iota_ y e. A ph )
                       -> A. x z e. ( iota_ y e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hblem hbreu hban hbiota
      ax-17 hbfv hbif hbxfreq ) BDACEHACEIZCJEKZALZCMZENOZPACEQUFBDUIUJABCEBDCE
      GRZFSUHBCDUGABUKFTUABDENDJNKBUBGUCUDUE $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.  $d z ps $.
    hbriotad.1 $e |- ( ph -> A. x ph ) $.
    hbriotad.2 $e |- ( ph -> A. y ph ) $.
    hbriotad.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbriotad.4 $e |- ( ph -> ( z e. A -> A. x z e. A ) ) $.
    $( Deduction version of ~ hbriota . $)
    hbriotad $p |- ( ph -> ( z e. ( iota_ y e. A ps )
                       -> A. x z e. ( iota_ y e. A ps ) ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif wceq df-riota a1i ax-17 hblemd
      hbreud hband hbiotad a17d hbfvd hbifd hbxfrd ) ACEBDFKZBDFLZDMFNZBOZDPZFQ
      RZSZGULURTABDFUAUBAUMCEUPUQGABCDFGHACEDFGAEUCJUDZIUEAUOCDEGHAUNBCUSIUFUGA
      CEFQGAEMQNCUHJUIUJUK $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d x z A $.  $d y A $.  $d y z ph $.  $d x z ps $.
    cbvriota.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Change bound variable in a restricted description binder. $)
    cbvriota $p |- ( iota_ x e. A ph ) = ( iota_ y e. A ps ) $=
      ( vz wreu cv wcel wa cio cund cfv cif crio cbvreuv wsbc ax-17 sb8iota wb
      wal wceq sban ax-gen iotabi ax-mp clelsb3 anbi1i eleq1 sbequ sbie syl6bb
      anbi12d hbsb hban cbviotaf eqtri 3eqtri eqid ifbieq12i df-riota 3eqtr4i
      ) ACEHZCIEJZAKZCLZEMNZOBDEHZDIZEJZBKZDLZVHOACEPBDEPVDVIVGVHVMVHABCDEFQVGV
      FCGIZRZGLZVECVNRZACVNRZKZGLZVMVFCGVFGSTVOVSUAZGUBVPVTUCWAGVEACGUDUEVOVSGU
      FUGVTVNEJZVRKZGLZVMVSWCUAZGUBVTWDUCWEGVQWBVRGCEUHUIUEVSWCGUFUGWCVLGDVNVJU
      CZWBVKVRBVNVJEUJWFVRACVJRBAGDCUKABCDBCSFULUMUNWBVRDWBDSACGDADSUOUPVLGSUQU
      RUSVHUTVAACEVBBDEVBVC $.
      $( [18-Mar-2013] $)
  $}

  ${
    $d y z A $.  $d x z w B $.  $d w z ph $.  $d w x y $.
    $( Interchange class substitution and restricted description binder. $)
    csbriotag $p |- ( A e. V
      -> [_ A / x ]_ ( iota_ y e. B ph ) = ( iota_ y e. B [ A / x ] ph ) ) $=
      ( vz vw cv crio csb wsbc wceq csbeq1 dfsbcq riotabidv eqeq12d visset
      hbs1 wcel ax-17 hbriota sbceq1a csbief vtoclg ) BGIZACEJZKZABUFLZCEJZMBDU
      GKZABDLZCEJZMGDFUFDMZUHUKUJUMBUFDUGNUNUIULCEABUFDOPQBHUFUGUJGRUIBCHEABGSH
      IETBUAUBBIUFMAUICEABUFUCPUDUE $.
      $( [24-Feb-2013] $)
  $}

  ${
    $d x A $.
    $( Closure of restricted iota. $)
    riotacl $p |- ( E! x e. A ph -> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio cv wcel wa cio riotaiota reiotacl eqeltrd ) ABCDABCEBFCGAHBIC
      ABCJABCKL $.
      $( [16-Jan-2012] $)
  $}

  $( Restricted iota equals the undefined value of its domain of discourse
     ` A ` when not meaningful. $)
  riotaund $p |- ( -. E! x e. A ph
                    -> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
    ( wreu wn crio cv wcel wa cio cund cfv cif df-riota iffalse syl5eq ) ABCDZE
    ABCFQBGCHAIBJZCKLZMSABCNQRSOP $.
    $( [16-Jan-2012] $)

  ${
    $d x A $.
    riotaclb.1 $e |- A e. _V $.
    $( Closure of restricted iota. $)
    riotaclb $p |- ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio wcel riotacl wn cund cfv undefnel2 cv wa cio cif df-riota
      iffalse syl5eq eleq1d mtbiri con4i impbii ) ABCEZABCFZCGZABCHUDUFUDIZUFCJ
      KZCGCDLUGUEUHCUGUEUDBMCGANBOZUHPUHABCQUDUIUHRSTUAUBUC $.
      $( [15-Sep-2011] $)

    $( Restricted iota equals the undefined value of its domain of discourse
       ` A ` when not meaningful. $)
    riotaundb $p |- ( -. E! x e. A ph
                      <-> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
      ( wreu wn crio cund cfv wceq riotaund wcel undefnel2 riotaclb eleq1
      syl5bb mtbiri impbii ) ABCEZFABCGZCHIZJZABCKUBSUACLZCDMSTCLUBUCABCDNTUACO
      PQR $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Closure of restricted iota. $)
    riotaclbg $p |- ( A e. V
          -> ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) ) $=
      ( vy cv wreu crio wcel wb wceq reueq1 id riotaeqdv eleq12d bibi12d
      visset riotaclb vtoclg ) ABEFZGZABTHZTIZJABCGZABCHZCIZJECDTCKZUAUDUCUFABT
      CLUGUBUETCUGABTCUGMZNUHOPABTEQRS $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d x ph $.
    riotabidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (deduction
       rule).  (Th. ~ rabbidva analog.) $)
    riotabidva $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu crio wceq wa cv wcel cio pm5.32da iotabidv adantr riotaiota
      adantl reubidva syl6bi imp 3eqtr4d wn cund cfv riotaund notbid eqtr4d
      pm2.61dan ) ABDEGZBDEHZCDEHZIAUJJDKELZBJZDMZUMCJZDMZUKULAUOUQIUJAUNUPDAUM
      BCFNOPUJUKUOIABDEQRAUJULUQIZAUJCDEGZURABCDEFSZCDEQTUAUBAUJUCZJUKEUDUEZULV
      AUKVBIABDEUFRAVAULVBIZAVAUSUCVCAUJUSUTUGCDEUFTUAUHUI $.
      $( [17-Jan-2012] $)
  $}

  ${
    riotabiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Equivalent wff's yield equal restricted iotas (inference rule).  (Th.
       ~ rabbiia analog.) $)
    riotabiia $p |- ( iota_ x e. A ph ) = ( iota_ x e. A ps ) $=
      ( cvv wceq crio eqid cv wcel wb adantl riotabidva ax-mp ) FFGZACDHBCDHGFI
      PABCDCJDKABLPEMNO $.
      $( [16-Jan-2012] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  riota4 $p |- ( E! x e. A ph -> [ ( iota_ x e. A ph ) / x ] ph ) $=
    ( wreu crio wsbc cv wcel wa cio reiota4 wceq wb riotaiota dfsbcq syl
    mpbird ) ABCDZABABCEZFZABBGCHAIBJZFZABCKRSUALTUBMABCNABSUAOPQ $.
    $( [16-Sep-2011] $)

  ${
    $d x y A $.
    $( Membership law for "the unique element in ` A ` such that ` ph ` ."
       (Th. ~ reucl2 analog.) $)
    riotacl2 $p |- ( E! x e. A ph
            -> ( iota_ x e. A ph ) e. { x e. A | ph } ) $=
      ( vy wreu crio wcel wsbc crab riotacl riota4 cv ax-17 elrabsf sylanbrc )
      ABCEABCFZCGABPHPABCIGABCJABCKABDPCDLCGBMNO $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d y ph $.  $d y ps $.  $d x y A $.  $d y B $.
    riota2df.1 $e |- ( ph -> A. x ph ) $.
    riota2df.2 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    riota2df.3 $e |- ( ( ph /\ B e. A ) -> ( ch -> A. x ch ) ) $.
    riota2df.4 $e |- ( ph -> ( x = B -> ( ps <-> ch ) ) ) $.
    $( A deduction version of ~ riota2f . $)
    riota2df $p |- ( ( ph /\ B e. A /\ E! x e. A ps ) ->
            ( ch <-> ( iota_ x e. A ps ) = B ) ) $=
      ( wcel wreu w3a cv wa cio wceq crio reiota2df wb riotaiota eqeq1d
      3ad2ant3 bitr4d ) AGFLZBDFMZNCDOFLBPDQZGRZBDFSZGRZABCDEFGHIJKTUGAUKUIUAUF
      UGUJUHGBDFUBUCUDUE $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    riota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( This theorem shows a condition that allows us to represent a descriptor
       with a class expression ` B ` .  The second hypothesis is a weakened
       bound variable condition that allows ~ hbsbc1g to be used.  Compare
       ~ reuuni2f . $)
    riota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota_ x e. A ph ) = B ) ) $=
      ( wcel wreu wa cv cio wceq crio reiota2f wb riotaiota eqeq1d adantl
      bitr4d ) FEJZACEKZLBCMEJALCNZFOZACEPZFOZABCDEFGHIQUDUHUFRUCUDUGUEFACESTUA
      UB $.
      $( [11-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x ph $.  $d y ps $.
    riota5f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota5f.2 $e |- ( ( ph /\ B e. A /\ x e. A ) -> ( ps <-> x = B ) ) $.
    $( A method for computing restricted iota. $)
    riota5f $p |- ( ( ph /\ B e. A ) -> ( iota_ x e. A ps ) = B ) $=
      ( wcel wa wsbc crio wceq cv wex cvv elisset issetf sylib adantl wi wal
      ax-17 hbel hban simpr simpll simplr eleq1a imp adantll w3a biimparc
      syl13anc wb sbceq1a mpbid ex 19.21ai hbsbc1g 19.23t syl mpd wreu wral
      wrex 3expia r19.21ai hbeleq eqeq2 bibi2d ralbid rcla4ev syl11anc reu6
      sylibr riota2f ) AFEIZJZBCFKZBCELFMZVSCNZFMZCOZVTVRWDAVRFPIWDFEQCDFGRSTVS
      WCVTUAZCUBZWDVTUAZVSWECAVRCACUCCDDFEGDNZEICUCUDZUEZVSWCVTVSWCJZBVTWKWCAVR
      WBEIZBVSWCUFAVRWCUGAVRWCUHVRWCWLAVRWCWLFEWBUIUJUKAVRWLULBWCHUMUNWCBVTUOVS
      BCFUPZTUQURUSVSVTVTCUBUAZCUBZWFWGUOVRWOAVRWNCWIBCDFEGUTZUSTWCVTCVAVBUQVCV
      SVRBCEVDZVTWAUOAVRUFZVSBWBWHMZUOZCEVEZDEVFZWQVSVRBWCUOZCEVEZXBWRVSXCCEWJA
      VRWLXCHVGVHXAXDDFEWHFMZWTXCCECDFGVIXEWSWCBWHFWBVJVKVLVMVNBCDEVOVPBVTCDEFG
      WPWMVQVNUQ $.
      $( [16-Apr-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ph $.  $d y ps $.
    riota5.1 $e |- ( ( ph /\ B e. A /\ x e. A ) -> ( ps <-> x = B ) ) $.
    $( A method for computing restricted iota. $)
    riota5 $p |- ( ( ph /\ B e. A ) -> ( iota_ x e. A ps ) = B ) $=
      ( vy cv wcel ax-17 riota5f ) ABCGDEGHEICJFK $.
      $( [20-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class.  Compare
       ~ reuuniss2 . $)
    riotass2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) )
         -> ( iota_ x e. A ph ) = ( iota_ x e. B ps ) ) $=
      ( wss wi wral wa wrex wreu cv wcel cio crio reiotass2 wceq reuss2
      riotaiota syl ad2antll 3eqtr4d ) DEFABGCDHIZACDJZBCEKZIIZCLZDMAICNZUGEMBI
      CNZACDOZBCEOZABCDEPUFACDKUJUHQABCDERACDSTUEUKUIQUCUDBCESUAUB $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d x y ph $.  $d ps y z $.
    $d ch x z $.
    riotaxfrd.1 $e |- ( z e. C -> A. y z e. C ) $.
    riotaxfrd.2 $e |- ( ( ph /\ y e. A ) -> B e. A ) $.
    riotaxfrd.3 $e |- ( ( ph /\ ( iota_ y e. A ch ) e. A ) -> C e. A ) $.
    riotaxfrd.4 $e |- ( x = B -> ( ps <-> ch ) ) $.
    riotaxfrd.5 $e |- ( y = ( iota_ y e. A ch ) -> B = C ) $.
    riotaxfrd.6 $e |- ( ( ph /\ x e. A ) -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` x ` such
       that ` ps ` " to another variable ` y ` contained in expression ` B ` .
       Use ~ reuhypd to eliminate the last hypothesis.  (Th. ~ reuunixfr
       analog.) $)
    riotaxfrd $p |- ( ( ph /\ E! x e. A ps ) -> ( iota_ x e. A ps ) = C ) $=
      ( wreu wa crio cv crab wcel rabid baib riotabiia wceq reuxfrd riotacl2
      adantl wb riotacl hbriota1 rabxfrd sylan2 mpbird ex sylbid imp syl5
      baibr reubiia biimpi ax-17 wal wi hbrab1 hbel a1i eleq1 riota2f syl11anc
      mpbid syl5eqr ) ABDGPZQZBDGRDSZBDGTZUAZDGRZIVQBDGVQVOGUAZBBDGUBZUCUDVNIVP
      UAZVRIUEZAVMWAAVMCEGPZWAABCDEHGKOMUFZAWCWAAWCQWACEGRZCEGTUAZWCWFACEGUGUHW
      CAWEGUAZWAWFUICEGUJZABCDEFHWEIGCEFGUKJKMNULUMUNUOUPUQVNIGUAZVQDGPZWAWBUIA
      VMWIAVMWCWIWDWCWGAWIWHAWGWILUOURUPUQVMWJAVMWJBVQDGVQVSBVTUSUTVAUHVQWADFGI
      FSIUADVBZWAWADVCVDWIDFFIVPWKBDFGVEVFVGVOIVPVHVIVJVKVL $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasvd.1 $e |- ( ph -> A. x ph ) $.
    riotasvd.2 $e |- ( ph -> A. y ph ) $.
    riotasvd.3 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasvd.4 $e |- ( ph -> D e. A ) $.
    $( Deduction version of ~ riotasv . $)
    riotasvd $p |- ( ( ph /\ A e. V ) -> ( ( y e. B /\ ps ) -> D = C ) ) $=
      ( vz wcel wa cv wceq wi wral crio eqcomd adantr wreu wb simpl eleq1d
      riotaclbg biimprd adantl sylbid mpd wal hbriota1 a1i hbxfrd a17d hbeqd
      hbimd hbrald a1d hbra1 hbriotad hband anabsi5 eqeq1 imbi2d ralbid ex
      riota2df syl111anc mpbird ra4 syl imp3a ) AEIOZPZDQFOZBHGRZVQBVSSZDFTZVRV
      TSVQWABCQZGRZSZDFTZCEUAZHRZAWGVPAHWFLUBUCVQAHEOZWECEUDZWAWGUEAVPUFAWHVPMU
      CZVQWHWIWJVQWHWFEOZWIAWHWKUEVPAHWFELUGUCVPWKWISAVPWIWKWECEIUHUIUJUKULAWEW
      ACNEHJACNHWFJLNQZWFOZWMCUMSAWECNEUNUOUPZAWAWACUMSWHAVTCDFJKAVRCUQABVSCJAB
      CUQACNNHGJWNAWLGOCUQURUSUTUCAWBHRZWEWAUEAWOPZWDVTDFAWOWPDUMAAWODAADUMAKVA
      ADNNWBHKAWLWBODUQADNHWFKLAWEDCNEKJWEWEDUMSAWDDFVBUOAWLEODUQVCUPURVDVEWPWC
      VSBWOWCVSUEAWBHGVFUJVGVHVIVJVKVLVTDFVMVNVO $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasvdOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasvdOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasvdOLD.3 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    $( Deduction version of ~ riotasv . $)
    riotasvdOLD $p |- ( ( ( ph /\ A e. V ) /\ D e. A /\ ( y e. B /\ ps ) )
         -> D = C ) $=
      ( vz wcel wa cv wceq wi wral crio eqcomd ad2antrr wreu wb eleq1d adantr
      riotaclbg biimprd adantl sylbid imp wal hbriota1 a1i hbxfrd a17d hbeqd
      hbimd hbrald a1d hbra1 hbriotad hband anabsi5 eqeq1 imbi2d ralbid ex
      riota2df 3adant1r mpd3an3 mpbird ra4 syl6 imp4a 3imp ) AEINZOZHENZDPFNZBO
      HGQZVRVSVTBWAVRVSBWARZDFSZVTWBRVRVSWCVRVSOWCBCPZGQZRZDFSZCETZHQZAWIVQVSAH
      WHLUAUBVRVSWGCEUCZWCWIUDZVRVSWJVRVSWHENZWJAVSWLUDVQAHWHELUEUFVQWLWJRAVQWJ
      WLWGCEIUGUHUIUJUKAVSWJWKVQAWGWCCMEHJACMHWHJLMPZWHNZWNCULRAWGCMEUMUNUOZAWC
      WCCULRVSAWBCDFJKAVTCUPABWACJABCUPACMMHGJWOAWMGNCUPUQURUSUFAWDHQZWGWCUDAWP
      OZWFWBDFAWPWQDULAAWPDAADULAKUTADMMWDHKAWMWDNDUPADMHWHKLAWGDCMEKJWGWGDULRA
      WFDFVAUNAWMENDUPVBUOUQVCVDWQWEWABWPWEWAUDAWDHGVEUIVFVGVHVIVJVKVLVHWBDFVMV
      NVOVP $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d y z E $.
    $d z F $.  $d z ph $.  $d x z ps $.
    riotasv2d.1 $e |- ( ph -> A. x ph ) $.
    riotasv2d.2 $e |- ( ph -> A. y ph ) $.
    riotasv2d.3 $e |- ( ph -> ( z e. F -> A. y z e. F ) ) $.
    riotasv2d.4 $e |- ( ph -> ( ch -> A. y ch ) ) $.
    riotasv2d.5 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv2d.6 $e |- ( ph -> ( y = E -> ( ps <-> ch ) ) ) $.
    riotasv2d.7 $e |- ( ph -> ( y = E -> C = F ) ) $.
    riotasv2d.8 $e |- ( ph -> D e. A ) $.
    riotasv2d.9 $e |- ( ph -> E e. B ) $.
    riotasv2d.10 $e |- ( ph -> ch ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv2d $p |- ( ( ph /\ A e. V ) -> D = F ) $=
      ( wcel cvv wceq elisset wa adantr cv wal wi wb ax-17 gen2 a1i a17d hband
      wral crio hbra1 hbriotad hbxfrd hbeqd hbimd 19.21ai eleq1 adantl imp
      anbi12d eqeq2d imbi12d ex hban riotasvd vtoclgft syl221anc mp2and sylan2
      ) GMUDAGUEUDZJLUFZGMUGAVTUHZKHUDZCWAAWCVTUBUIZACVTUCUIWBFUJZKUDZWFEUKULZF
      UKEUKZWCCUHZWAULZWJEUKULZEUKZEUJZKUFZWMHUDZBUHZJIUFZULZWJUMZULZEUKZWREUKW
      CWJWHWBWGEFWFEUNUOUPAWLVTAWKEOAWIWAEOAWCCEAWCEUQQURAEFFJLOAEFJBDUJIUFULZE
      HUSZDGUTORAXCEDFGONXCXCEUKULAXBEHVAUPAWEGUDEUQVBVCPVDVEVFUIAXAVTAWTEOAWNW
      SAWNUHZWPWIWQWAXDWOWCBCWNWOWCUMAWMKHVGVHAWNBCUMSVIVJXDILJAWNILUFTVIVKVLVM
      VFUIWBWREAVTEOVTEUNVNABDEGHIJUENORUAVOVFWDWRWJEFKHVPVQVRVS $.
      $( [2-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d y z E $.
    $d z F $.  $d z ph $.  $d x z ps $.
    riotasv2dOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasv2dOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasv2dOLD.3 $e |- ( ph -> ( z e. F -> A. y z e. F ) ) $.
    riotasv2dOLD.4 $e |- ( ph -> ( ch -> A. y ch ) ) $.
    riotasv2dOLD.5 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv2dOLD.6 $e |- ( ph -> ( y = E -> ( ps <-> ch ) ) ) $.
    riotasv2dOLD.7 $e |- ( ph -> ( y = E -> C = F ) ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv2dOLD $p |- ( ( ( ph /\ A e. V ) /\ ( D e. A /\ E e. B /\ ch ) )
             -> D = F ) $=
      ( wcel cvv w3a wceq elisset wa wi cv wal wb ax-17 gen2 a1i wral crio
      hbra1 a17d hbriotad hbxfrd hbeld hb3and hbeqd hbimd 19.21ai ad2antrr
      eleq1 adantl imp 3anbi23d eqeq2d imbi12d ex hban riotasvdOLD 3exp exp4a
      3impd adantr simpr vtoclgft syl221anc 3expd com23 pm2.43d 3imp2 sylanl2
      ) GMUAAGUBUAZJGUAZKHUAZCUCZJLUDZGMUEAWGUFZWHWICWKWLWIWHCWKUGZWLWIWHWMUGZW
      LWIWIWNUGWLWIUFZWHWIWMWOWHWICWKWOFUHZKUAZWQEUIUGZFUIEUIZWJWKUGZWTEUIUGZEU
      IZEUHZKUDZWHXCHUAZBUCZJIUDZUGZWTUJZUGZEUIZXHEUIZWIWTWSWOWREFWQEUKULUMAXBW
      GWIAXAEOAWJWKEOAWHWICEAEFJGOAEFJBDUHIUDUGZEHUNZDGUOORAXNEDFGONXNXNEUIUGAX
      MEHUPUMAWPGUAEUQZURUSZXOUTAWIEUQQVAAEFFJLOXPPVBVCVDVEAXKWGWIAXJEOAXDXIAXD
      UFZXFWJXGWKXQXEWIBCWHXDXEWIUJAXCKHVFVGAXDBCUJSVHVIXQILJAXDILUDTVHVJVKVLVD
      VEWLXLWIWLXHEAWGEOWGEUKVMWLWHXEBXGWLWHXEBXGWLWHXEBUFXGABDEGHIJUBNORVNVOVP
      VQVDVRWLWIVSXHWTEFKHVTWAWBWCVLWDWCWEWF $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d x y z E $.
    $d x z ph $.
    riotasv2s.2 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    $( The value of description binder ` D ` for a single-valued class
       expression ` C ( y ) ` (as in e.g. ~ reusv2 ) in the form of a
       substitution instance.  Special case of ~ riota2f . $)
    riotasv2s $p |- ( ( A e. V /\ D e. A /\ ( E e. B /\ [ E / y ] ph ) )
            -> D = [_ E / y ]_ C ) $=
      ( vz wcel wsbc wa w3a csb wceq 3simpc simp1 cv wi wral crio hbriota1
      hbxfreq ax-17 hbel hban wal hbra1 hbriota a1i a17d hbsbc1g hband exp4d
      pm2.43b imp32 hbcsb1g ad2antrl wb sbceq1a csbeq1a simpl simprl simprr
      riotasv2d syl11anc ) DILZGDLZHELZACHMZNZOVJVMNZVIGCHFPZQVIVJVMRVIVJVMSVNA
      VLBCKDEFGHVOIVJVMBBKKGDBKGABTFQUAZCEUBZBDUCZJVQBKDUDUEKTZDLZBUFUGVMBUFUHV
      JVKVLVNCUIZVJVKVLWAUAVKVJVKVLWAVKVJVMCVJVJCUIUAVKCKKGDCKGVRJVQCBKDVPCEUJV
      TCUFZUKUEWBUGULVKVKVLCVKVKCUMACKHEVSHLCUFZUNZUOUOUPUQURVKVSVOLZWECUIUAVJV
      LCKHFEWCUSUTVKVLVLCUIUAVJVLWDUTGVRQVNJULCTHQZAVLVAUAVNACHVBULWFFVOQUAVNCH
      FVCULVJVMVDVJVKVLVEVJVKVLVFVGVH $.
      $( [3-Mar-2013] $)

    $( The value of description binder ` D ` for a single-valued class
       expression ` C ( y ) ` (as in e.g. ~ reusv2 ) in the form of a
       substitution instance.  Special case of ~ riota2f . $)
    riotasv2sOLD $p |- ( ( A e. V /\ D e. A /\ ( E e. B /\ [ E / y ] ph ) )
            -> D = [_ E / y ]_ C ) $=
      ( vz wcel wsbc wa w3a csb wceq simp3l simp1 simp2 simp3r ax-17 cv
      hbcsb1g hbsbc1g wi wral crio a1i wb sbceq1a csbeq1a riotasv2dOLD
      syl23anc ) DILZGDLZHELZACHMZNZOUQUOUPUQURGCHFPZQUOUPUQURRZUOUPUSSUOUPUSTV
      AUOUPUQURUAUQAURBCKDEFGHUTIUQBUBUQCUBCKHFEKUCHLCUBZUDACKHEVBUEGABUCFQUFCE
      UGBDUHQUQJUICUCHQZAURUJUFUQACHUKUIVCFUTQUFUQCHFULUIUMUN $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d x z ph $.  $d z D $.
    riotasv.1 $e |- A e. _V $.
    riotasv.2 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv $p |- ( ( D e. A /\ y e. B /\ ph ) -> D = C ) $=
      ( vz wcel cv wceq cvv wa wi wral crio hbriota1 hbxfreq ax-17 hbel hbra1
      hbriota a1i id riotasvd mpan2 3impib ) GDKZCLEKZAGFMZUJDNKUKAOULPHUJABCDE
      FGNBJJGDBJGABLFMPZCEQZBDRZIUNBJDSTJLDKZBUAUBCJJGDCJGUOIUNCBJDUMCEUCUPCUAZ
      UDTUQUBGUOMUJIUEUJUFUGUHUI $.
      $( [26-Jan-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasv3d.1 $e |- ( ph -> A. x ph ) $.
    riotasv3d.2 $e |- ( ph -> A. y ph ) $.
    riotasv3d.3 $e |- ( ph -> ( th -> A. y th ) ) $.
    riotasv3d.4 $e |- ( ph
           -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv3d.5 $e |- ( ph -> ( C = D -> ( ch <-> th ) ) ) $.
    riotasv3d.6 $e |- ( ph -> ( ( y e. B /\ ps ) -> ch ) ) $.
    riotasv3d.7 $e |- ( ph -> D e. A ) $.
    riotasv3d.8 $e |- ( ph -> E. y e. B ps ) $.
    $( A property ` ch ` holding for a representative of a single-valued class
       expression ` C ( y ) ` (see e.g. ~ reusv2 ) also holds for its
       description binder ` D ` (in the form of property ` th ` ). $)
    riotasv3d $p |- ( ( ph /\ A e. V ) -> th ) $=
      ( wcel cvv elisset wa wrex adantr wi wral ax-17 cv imp adantrl wb wceq
      riotasvd eqcom syl6ib expimpd syld mpbid exp45 r19.21ad wal 19.21ai
      r19.23t syl sylibd mpd sylan2 ) GKTAGUATZDGKUBAVIUCZBFHUDZDAVKVISUEAVIVKD
      UFZAVIBDUFZFHUGZVLAVIVMFHMVIFUHAVIFUIHTZBDAVIVOBUCZUCZUCCDAVPCVIAVPCQUJUK
      AVQCDULZAVQIJUMZVRAVIVPVSVJVPJIUMVSABEFGHIJUALMORUNJIUOUPUQPURUJUSUTVAADD
      FVBUFZFVBVNVLULAVTFMNVCBDFHVDVEVFUJVGVH $.
      $( [5-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasv3dOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasv3dOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasv3dOLD.3 $e |- ( ph -> ( th -> A. y th ) ) $.
    riotasv3dOLD.4 $e |- ( ph
           -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv3dOLD.5 $e |- ( ph -> ( C = D -> ( ch <-> th ) ) ) $.
    riotasv3dOLD.6 $e |- ( ph -> ( ( y e. B /\ ps ) -> ch ) ) $.
    $( A property ` ch ` holding for a representative of a single-valued class
       expression ` C ( y ) ` (see e.g. ~ reusv2 ) also holds for its
       description binder ` D ` (in the form of property ` th ` ). $)
    riotasv3dOLD $p |- ( ( ph /\ ( A e. V /\ D e. A /\ E. y e. B ps ) )
          -> th ) $=
      ( vz wcel wrex cvv wi elisset wa wral cv imp adantrl wb wceq w3a
      riotasvdOLD eqcomd 3exp ex imp4c syld mpbid exp45 com23 r19.21ai wal
      a17d crio hbra1 ax-17 hbriota a1i hbxfrd hbeld hband 19.21ai r19.21t syl
      r19.23t sylibd exp3a syl5 3imp2 ) AGKSZJGSZBFHTZDVTGUASZAWAWBDUBZUBGKUCAW
      CWAWDAWCWAUDZBDUBZFHUEZWDAWEWFUBZFHUEZWEWGUBZAWHFHMAWEFUFHSZWFAWEWKBDAWEW
      KBUDZUDZUDCDAWLCWEAWLCQUGUHAWMCDUIZAWMIJUJZWNAWCWAWLWOAWCWAWLWOUBUBAWCUDZ
      WAWLWOWPWAWLUKJIABEFGHIJUALMOULUMUNUOUPPUQUGURUSUTVAAWEWEFVBUBZFVBWIWJUIA
      WQFMAWCWAFAWCFVCAFRJGMAFRJBEUFIUJUBZFHUEZEGVDZMORUFZWTSZXBFVBUBAWSFERGWRF
      HVEXAGSZFVFVGVHVIAXCFVCVJVKVLWEWFFHVMVNURADDFVBUBZFVBWGWDUIAXDFMNVLBDFHVO
      VNVPVQVRVS $.
      $( [1-Feb-2013] $)
  $}


