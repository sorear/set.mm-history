$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Transfinite induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( The Principle of Transfinite Induction.  Theorem 7.17 of [TakeutiZaring]
       p. 39.  This principle states that if ` A ` is a class of ordinal
       numbers with the property that every ordinal number included in ` A `
       also belongs to ` A ` , then every ordinal number is in ` A ` .

       See theorem ~ tfindes or ~ tfinds for the version involving basis and
       induction hypotheses. $)
    tfi $p |- ( ( A C_ On /\ A. x e. On ( x C_ A -> x e. A ) ) -> A = On ) $=
      ( con0 wss cv wcel wi wral wa wceq cdif cin c0 wrex wn eldifn adantl onss
      eldifi difin0ss syl5com imim1d a2i syl5 imp mtod ralimi2 ralnex sylib wne
      ssdif0 necon3bbii word ordon difss tz7.5 mp3an12 sylbi anim2i eqss sylibr
      ex nsyl2 ) BCDZAEZBDZVEBFZGZACHZIVDCBDZIBCJVIVJVDVICBKZVELMJZAVKNZVJVIVLO
      ZAVKHVMOVHVNACVKVECFZVHGZVEVKFZVNVPVQIVLVGVQVGOVPVECBPQVPVQVLVGGZVQVOVPVR
      VECBSVOVHVRVOVLVFVGVOVECDVLVFVERCBVETUAUBUCUDUEUFVBUGVLAVKUHUIVJOVKMUJZVM
      VJVKMCBUKULCUMVKCDVSVMUNCBUOACVKUPUQURVCUSBCUTVA $.
      $( [18-Feb-2004] $)
  $}

  ${
    $d y ph z $.  $d x y z $.
    tfis.1 $e |- ( x e. On -> ( A. y e. x [ y / x ] ph -> ph ) ) $.
    $( Transfinite Induction Schema.  If all ordinal numbers less than a given
       number ` x ` have a property (induction hypothesis), then all ordinal
       numbers have the property (conclusion).  Exercise 25 of [Enderton]
       p. 200. $)
    tfis $p |- ( x e. On -> ph ) $=
      ( vz cv con0 wcel crab wss wral wceq wsbc ax-17 elrabsf simprbi hbs1 hbim
      wi imbi12d ssrab2 dfss3 ralimi sylbi hbral eleq1 raleq sbequ12 chvar syl5
      wa anc2li syl6ibr rgen tfi mp2an eqcomi rabeq2i ) BFZGHZUTAABGGABGIZGVAGJ
      EFZVAJZVBVAHZSZEGKVAGLABGUAVEEGVBGHZVCVFABVBMZUKVDVFVCVGVCABCFZMZCVBKZVFV
      GVCVHVAHZCVBKVJCVBVAUBVKVICVBVKVHGHVIABEVHGVFBNZOPUCUDUTVICUSKZASZSVFVJVG
      SZSBEVFVOBVLVJVGBVIBCVBVHVBHBNABCQUEABEQRRUSVBLZUTVFVNVOUSVBGUFVPVMVJAVGV
      ICUSVBUGABEUHTTDUIUJULABEVBGVLOUMUNEVAUOUPUQURP $.
      $( [1-Mar-2004] $) $( [1-Aug-1994] $)
  $}

  ${
    $d y ph $.  $d x y $.
    tfis2f.1 $e |- ( ps -> A. x ps ) $.
    tfis2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2f.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema, using implicit substitition. $)
    tfis2f $p |- ( x e. On -> ph ) $=
      ( cv wsbc wral con0 wcel sbie ralbii syl5bi tfis ) ACDACDHIZDCHZJBDRJRKLA
      QBDRABCDEFMNGOP $.
      $( [1-Mar-2004] $) $( [18-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x y $.
    tfis2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2.2 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema, using implicit substitition. $)
    tfis2 $p |- ( x e. On -> ph ) $=
      ( ax-17 tfis2f ) ABCDBCGEFH $.
      $( [1-Mar-2004] $) $( [18-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x ch $.  $d x A $.  $d x y $.
    tfis3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis3.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    tfis3.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema, using implicit substitition. $)
    tfis3 $p |- ( A e. On -> ch ) $=
      ( con0 tfis2 vtoclga ) ACDFJHABDEGIKL $.
      $( [4-Nov-2003] $)
  $}

  ${
    $d x y z $.  $d x A $.  $d x z ch $.  $d x ta $.  $d y z ph $.
    $( Substitutions. $)
    tfinds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds.5 $e |- ps $.
    $( Induction hypothesis for successors. $)
    tfinds.6 $e |- ( y e. On -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds.7 $e |- ( Lim x -> ( A. y e. x ch -> ph ) ) $.
    $( Principle of Transfinite Induction (inference schema), using implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last three are the basis, the induction hypothesis for
       successors, and the induction hypothesis for limit ordinals.  Theorem
       Schema 4 of [Suppes] p. 197.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    tfinds $p |- ( A e. On -> ta ) $=
      ( vz con0 wral wi ax-17 cv wcel wlim wn word wceq csuc wrex dflim3 notbii
      c0 wo wa iman eloni pm2.27 syl mpbiri a1d hbra1 visset sucid rcla4v ax-mp
      hbim syl5 wsbc raleq sbie sbequ syl5bbr cbvral sbequ12 3bitr4g syl5ibrcom
      hbs1 imbi1d biimprd syldd rexlimi jaoi syl6 syl5bir syl5bi pm2.61d2 tfis3
      a1i ) ACEFGHJLFUAZQUBZWHUCZCGWHRZASZWJUDWHUEZWHUKUFZWHGUAZUGZUFZGQUHZULZU
      DUMZUDZWIWLWJWTGWHUIUJXAWMWSSZWIWLWMWSUNWIXBWSWLWIWMXBWSSWHUOWMWSUPUQWNWL
      WRWNAWKWNABMIURUSWQWLGQWKAGCGWHUTAGTVEWOQUBZWQWKDAXCWKDSWQAFWPRZDSXDCXCDW
      OWPUBXDCSWOGVAVBACFWOWPJVCVDNVFWQWKXDDWQAFPUAZVGZPWHRXFPWPRWKXDXFPWHWPVHC
      XFGPWHCPTXFGTCAFWOVGWOXEUFXFACFGCFTJVIAGPFVJVKVLAXFFPWPAPTAFPVPAFPVMVLVNV
      QVOWQDASSXCWQADKVRWGVSVTWAWBWCWDOWEWF $.
      $( [27-Aug-2011] $) $( [16-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x ch $.  $d x th $.  $d x ta $.  $d y ph $.
    $( Substitutions. $)
    tfindsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    tfindsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg.6 $e |- ( ( ( y e. On /\ B e. On ) /\ B C_ y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg.7 $e |- ( ( ( Lim x /\ B e. On ) /\ B C_ x ) ->
                   ( A. y e. x ( B C_ y -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema), using implicit substititions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` B ` instead of zero.  Remark in [TakeutiZaring]
       p. 57. $)
    tfindsg $p |- ( ( ( A e. On /\ B e. On ) /\ B C_ A ) -> ta ) $=
      ( wcel wi wceq wa con0 wss cv c0 csuc wb sseq2 adantl syl6bir imp imbi12d
      eqeq2 imbi1d ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61ian imbi2d a1d wex
      wn visset sucex eqvinc syl5ibr biimpd sylan9r exlimiv sylbi eqcoms imim2i
      com4r wne df-ne anbi2i annim bitri onsssuc suceloni onelpss sylan2 ancoms
      bitrd ex ax-1 syl8 com23 sylbird syl5bir pm2.61d wlim wral pm2.27 ralimdv
      a2d ad2antlr syld exp31 com3l com4t tfinds imp31 ) HUAQIUAQZIHUBZEXEIFUCZ
      UBZARZRXEIUDUBZBRZRXEIGUCZUBZCRZRZXEIXLUEZUBZDRZRXEXFERZRFGHXGUDSZXIXKXEI
      UDSZXTXIXKUFYAXTTXHXJABXTXHXJUFYAXGUDIUGZUHYAXTABUFZYAXTXGISZYCIUDXGULJUI
      UJUKXTXIXJARYAVCZXKXTXHXJAYBUMYEXJABYEXJYCXJYAIUNUOUPUQURUSUTXGXLSZXIXNXE
      YFXHXMACXGXLIUGKUKUTXGXPSZXIXRXEYGXHXQADXGXPIUGLUKUTXGHSZXIXSXEYHXHXFAEXG
      HIUGMUKUTXEBXJNVAXLUAQZXEXNXRYIXEXNXRRZYIXETZXQIXPSZRZYJXEYMYJRYIYMXNXQXE
      DYMXQXEDRZRXNYLYNXQYNXPIXPISYGYDTZFVBYNFXPIXLGVDVEVFYOYNFYDXEAYGDXEAYDBNJ
      VGYGADLVHVIVJVKVLVMVAVNUHYMVCZXQIXPVOZTZYKYJYRXQYLVCZTYPYQYSXQIXPVPVQXQYL
      VRVSYKYRXMYJXEYIXMYRUFXEYITXMIXPQZYRIXLVTYIXEXPUAQYTYRUFXLWAIXPWBWCWEWDYK
      XNXMXRYKXMCXRYKXMCDXRYKXMCDROWFDXQWGWHWQWIWJWKWLWFWQXEXHXGWMZXOGXGWNZAUUA
      XEXHUUBARZUUAXEXHUUCUUAXETXHTUUBXNGXGWNZAXEUUBUUDRUUAXHXEXOXNGXGXEXNWOWPW
      RPWSWTXAXBXCXD $.
      $( [5-Mar-2004] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x ch $.  $d x th $.  $d x ta $.  $d y ph $.
    $( Substitutions. $)
    tfindsg2.1 $e |- ( x = suc B -> ( ph <-> ps ) ) $.
    tfindsg2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg2.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg2.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg2.6 $e |- ( ( y e. On /\ B e. y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg2.7 $e |- ( ( Lim x /\ B e. x ) ->
                   ( A. y e. x ( B e. y -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema), using implicit substititions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` suc B ` instead of zero.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Jun-2012.) $)
    tfindsg2 $p |- ( ( A e. On /\ B e. A ) -> ta ) $=
      ( con0 wcel wa wi csuc wss onelon sucelon sylib eloni ordsucss syl sylbir
      word imp cv wb ordelsuc sylan2 ancoms ex adantr sylbird sylan2br wlim cvv
      wral visset limelon mpan anassrs imbi1d imbi12d mpbid tfindsg expl mp2and
      ralbidva ) HQRZIHRZSZIUAZQRZVRHUBZEVQIQRZVSHIUCIUDZUEVOVPVTVOHUJVPVTTHUFI
      HUGUHUKVOVSVTSETVPVOVSVTEABCDEFGHVRJKLMVSWABWBNUIGULZQRZVSSVRWCUBZCDTZVSW
      DWAWEWFTWBWDWASWEIWCRZWFWAWDWGWEUMZWDWAWCUJZWHWCUFZIWCQUNZUOUPWDWGWFTWAWD
      WGWFOUQURUSUTUKFULZVAZVSSVRWLUBZWECTZGWLVCZATZVSWMWAWNWQTZWBWMWASIWLRZWGC
      TZGWLVCZATZTZWRWMXCWAWMWSXBPUQURWAWMXCWRUMZWMWAWLQRZXDWLVBRWMXEFVDWLVBVEV
      FWAXESZWSWNXBWQXEWAWLUJWSWNUMWLUFIWLQUNUOXFXAWPAXFWTWOGWLXFWCWLRZSWGWECWA
      XEXGWHXEXGSZWAWIWHXHWDWIWLWCUCWJUHWKUOVGVHVNVHVIUOUPVJUTUKVKVLURVM $.
      $( [19-Jun-2012] $) $( [5-Jan-2005] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    tfindes.1 $e |- [ (/) / x ] ph $.
    tfindes.2 $e |- ( x e. On -> ( ph -> [ suc x / x ] ph ) ) $.
    tfindes.3 $e |- ( Lim y -> ( A. x e. y ph -> [ y / x ] ph ) ) $.
    $( Transfinite Induction with explicit substitution.  The first hypothesis
       is the basis, the second is the induction hypothesis for successors, and
       the third is the induction hypothesis for limit ordinals.  Theorem
       Schema 4 of [Suppes] p. 197. $)
    tfindes $p |- ( x e. On -> ph ) $=
      ( vz cv wsbc c0 csuc dfsbcq con0 wcel wi ax-17 hbim wceq imbi12d wral syl
      sbequ sbequ12r hbs1 visset sucex hbsbc1v eleq1 sbequ12 suceq chvar cbvral
      wb wlim syl5bir tfinds ) ABCHZIZABJIABGHZIZABUSKZIZACGBHZABUQJLACGBUBABUQ
      VALACBUCDVCMNZAABVCKZIZOZOUSMNZUTVBOZOBGVHVIBVHBPUTVBBABGUDZABVAUSGUEUFUG
      QQVCUSRZVDVHVGVIVCUSMUHVKAUTVFVBABGUIZVKVEVARVFVBUMVCUSUJABVEVALUASSEUKUT
      GUQTABUQTUQUNURAUTBGUQAGPVJVLULFUOUP $.
      $( [5-Mar-2004] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $.  $d y ph $.
    $( Substitutions. $)
    tfinds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    tfinds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis for successors. $)
    tfinds2.5 $e |- ( y e. On -> ( ta -> ( ch -> th ) ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds2.6 $e |- ( Lim x -> ( ta -> ( A. y e. x ch -> ph ) ) ) $.
    $( Transfinite Induction (inference schema), using implicit substititions.
       The first three hypotheses establish the substitutions we need.  The
       last three are the basis and the induction hypotheses (for successor and
       limit ordinals respectively).  Theorem Schema 4 of [Suppes] p. 197.  The
       wff ` ta ` is an auxiliary antecedent to help shorten proofs using this
       theorem. $)
    tfinds2 $p |- ( x e. On -> ( ta -> ph ) ) $=
      ( wi c0 wsbc wceq imbi2d sbcie con0 0ex cv mpbir wcel csuc a2d cvv visset
      sbimi sbcel1gv ax-mp sbim 3imtr3i bicomd equcoms sucex sbbii suceq sbcco2
      wb bitr3i 3imtr3g wral sbralie r19.21v syl5bi ax-17 limeq syl5bir tfindes
      wlim sbie ) EANZFGVMFOPEBNZKVMVNFOUAFUBZOQABEHRSUCVOTUDZECNZGVOPZEDNZGVOP
      ZVMVMFVOUEZPZGUBZTUDZGVOPZVQVSNZGVOPVPVRVTNWDWFGFWDECDLUFUIVOUGUDWEVPUTFU
      HZGVOTUGUJUKVQVSGFULUMVQVMGVOWGWCVOQCAECAUTFGVOWCQACIUNUORZSVTVMFWCUEZPZG
      VOPWBWJVSGFVMVSFWIWCGUHUPVOWIQADEJRSUQVMFGWAWIVOWCURUSVAVBVMFWCVCVQGVOVCZ
      FWCPZWCVKZVMFWCPZVQVMGFWHVDVOVKZFWCPWKVMNZFWCPWMWLWNNWOWPFGWKECGVOVCZNWOV
      MECGVOVEWOEWQAMUFVFUIWOWMFGWMFVGVOWCVHVLWKVMFGULUMVIVJ $.
      $( [4-Sep-2004] $)
  $}

  ${
    $d x A $.  $d y ph $.  $d x ch $.  $d x ta $.  $d x y et $.
    $( Substitutions. $)
    tfinds3.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds3.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds3.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds3.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds3.5 $e |- ( et -> ps ) $.
    $( Induction hypothesis for successors. $)
    tfinds3.6 $e |- ( y e. On -> ( et -> ( ch -> th ) ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds3.7 $e |- ( Lim x -> ( et -> ( A. y e. x ch -> ph ) ) ) $.
    $( Principle of Transfinite Induction (inference schema), using implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last three are the basis, the induction hypothesis for
       successors, and the induction hypothesis for limit ordinals.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 21-Jun-2011.) $)
    tfinds3 $p |- ( A e. On -> ( et -> ta ) ) $=
      ( wi cv wceq imbi2d c0 csuc con0 wcel a2d wral wlim r19.21v syl5bi tfinds
      ) FAQZFBQFCQZFDQFEQGHIGRZUASABFJTUMHRZSACFKTUMUNUBSADFLTUMISAEFMTNUNUCUDF
      CDOUEULHUMUFFCHUMUFZQUMUGZUKFCHUMUHUPFUOAPUEUIUJ $.
      $( [21-Jun-2011] $) $( [6-Jan-2005] $)
  $}

