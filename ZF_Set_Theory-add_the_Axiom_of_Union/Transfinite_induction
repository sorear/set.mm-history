$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Transfinite induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( The Principle of Transfinite Induction.  Theorem 7.17 of [TakeutiZaring]
       p. 39.  This principle states that if ` A ` is a class of ordinals with
       the property that every ordinal number included in ` A ` also belongs to
       ` A ` , then every ordinal is in ` A ` . $)
    tfi $p |- ( ( A (_ On /\ A. x e. On ( x (_ A -> x e. A ) ) -> A = On ) $=
      ( con0 wss cv wcel wi wral wa wceq cdif cin c0 wrex wn eldifn adantl
      difin0ss onsst syl5com imim1d a2i eldifi syl5 imp mtod ex r19.20i2
      ralnex sylib wne ssdif0 necon3bbii word ordon difss tz7.5 mp3an12 sylbi
      nsyl2 anim2i eqss sylibr ) BCDZAEZBDZVEBFZGZACHZIVDCBDZIBCJVIVJVDVICBKZVE
      LMJZAVKNZVJVIVLOZAVKHVMOVHVNACVKVECFZVHGZVEVKFZVNVPVQIVLVGVQVGOVPVECBPQVP
      VQVLVGGZVPVOVRVQVOVHVRVOVLVFVGVLVECDVFVOCBVERVESTUAUBVECBUCUDUEUFUGUHVLAV
      KUIUJVJOVKMUKZVMVJVKMCBULUMCUNVKCDVSVMUOCBUPACVKUQURUSUTVABCVBVC $.
      $( [29-Feb-2004] $) $( [18-Feb-2004] $)
  $}

  ${
    $d y ph z $.  $d x y z $.
    tfis.1 $e |- ( x e. On -> ( A. y e. x [ y / x ] ph -> ph ) ) $.
    $( Transfinite Induction Schema.  If all ordinal numbers less than a
       given number ` x ` have a property (induction hypothesis), then all
       ordinal numbers have the property (conclusion).  Exercise 25 of
       [Enderton] p. 200. $)
    tfis $p |- ( x e. On -> ph ) $=
      ( vz cv con0 wcel crab wss wi wral wceq ssrab2 wsbc wa ax-17 hbs1 hbral
      hbim eleq1 raleq1 sbequ12 imbi12d chvar dfss3 elrabsf pm3.27bi r19.20si
      sylbi syl5 anc2li syl6ibr rgen tfi mp2an eqcomi rabeq2i ) BFZGHZUTAABGGAB
      GIZGVAGJEFZVAJZVBVAHZKZEGLVAGMABGNVEEGVBGHZVCVFABVBOZPVDVFVCVGVFABCFZOZCV
      BLZVGVCUTVICUSLZAKZKVFVJVGKZKBEVFVMBVFBQZVJVGBVIBCVBVHVBHBQABCRSABERTTUSV
      BMZUTVFVLVMUSVBGUAVOVKVJAVGVICUSVBUBABEUCUDUDDUEVCVHVAHZCVBLVJCVBVAUFVPVI
      CVBVPVHGHVIABEVHGVNUGUHUIUJUKULABEVBGVNUGUMUNEVAUOUPUQURUH $.
      $( [1-Mar-2004] $) $( [1-Aug-1994] $)
  $}

  ${
    $d y ph $.  $d x y $.
    tfis2f.1 $e |- ( ps -> A. x ps ) $.
    tfis2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2f.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2f $p |- ( x e. On -> ph ) $=
      ( cv con0 wcel wral wsbc sbie ralbii syl5ib tfis ) ACDCHZIJBDQKAACDHLZDQK
      GRBDQABCDEFMNOP $.
      $( [1-Mar-2004] $) $( [18-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x y $.
    tfis2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2.2 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2 $p |- ( x e. On -> ph ) $=
      ( ax-17 tfis2f ) ABCDBCGEFH $.
      $( [1-Mar-2004] $) $( [18-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x ch $.  $d x A $.  $d x y $.
    tfis3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis3.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    tfis3.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis3 $p |- ( A e. On -> ch ) $=
      ( con0 tfis2 vtoclga ) ACDFJHABDEGIKL $.
      $( [6-Nov-2003] $) $( [4-Nov-2003] $)
  $}

