$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        "Maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |-> $.  $( Maps-to symbol $)

  $( Extend the definition of a class to include maps-to notation for defining
     a function via a rule. $)
  cmpt $a class ( x e. A |-> B ) $.

  $( Extend the definition of a class to include maps-to notation for defining
     an operation via a rule. $)
  cmpt2 $a class ( x e. A , y e. B |-> C ) $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define maps-to notation for defining a function via a rule.  Read as
       "the function defined by the map from ` x ` (in ` A ` ) to
       ` B ( x ) ` ."  The class expression ` B ` is the value of the function
       at ` x ` and normally contains the variable ` x ` .  An example is the
       square function for complex numbers, ` ( x e. CC |-> ( x ^ 2 ) ` .
       Similar to the definition of mapping in [ChoquetDD] p. 2. $)
    df-mpt $a |- ( x e. A |-> B ) =
                    { <. x , y >. | ( x e. A /\ y = B ) } $.
  $}

  ${
    $d x z $.  $d y z $.  $d z A $.  $d z B $.  $d z C $.
    $( Define maps-to notation for defining an operation via a rule.  Read as
       "the operation defined by the map from ` x , y ` (in ` A X. B ` ) to
       ` B ( x , y ) ` ."  An extension of ~ df-mpt for two arguments. $)
    df-mpt2 $a |- ( x e. A , y e. B |-> C ) =
             { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
  $}

  ${
    $d x y ph $.  $d y A $.  $d y B $.  $d y C $.  $d y D $.
    $( An equality theorem for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpteq12f $p |- ( ( A. x A = C /\ A. x e. A B = D ) ->
                    ( x e. A |-> B ) = ( x e. C |-> D ) ) $=
      ( vy wceq wal wral wa cv wcel copab cmpt hba1 hbra1 hban ax-17 ra4 df-mpt
      imp eqeq2d pm5.32da ax-4 eleq2d anbi1d sylan9bbr opabbid 3eqtr4g ) BDGZAH
      ZCEGZABIZJZAKZBLZFKZCGZJZAFMUODLZUQEGZJZAFMABCNADENUNUSVBAFUKUMAUJAOULABP
      QUNFRUMUSUPVAJUKVBUMUPURVAUMUPJCEUQUMUPULULABSUAUBUCUKUPUTVAUKBDUOUJAUDUE
      UFUGUHAFBCTAFDETUI $.
      $( [16-Dec-2013] $)

    mpteq12dv.1 $e |- ( ph -> A = C ) $.
    mpteq12dv.2 $e |- ( ph -> B = D ) $.
    $( An equality inference for the maps to notation. $)
    mpteq12dv $p |- ( ph -> ( x e. A |-> B ) = ( x e. C |-> D ) ) $=
      ( wceq wal wral cmpt 19.21aiv cv wcel a1d r19.21aiv mpteq12f syl11anc )
      ACEIZBJDFIZBCKBCDLBEFLIATBGMAUABCAUABNCOHPQBCDEFRS $.
      $( [16-Dec-2013] $) $( [24-Aug-2011] $)
  $}

  ${
    $d x A $.  $d x C $.
    $( An equality theorem for the maps to notation. $)
    mpteq12 $p |- ( ( A = C /\ A. x e. A B = D ) ->
                    ( x e. A |-> B ) = ( x e. C |-> D ) ) $=
      ( wceq wal wral cmpt ax-17 mpteq12f sylan ) BDFZMAGCEFABHABCIADEIFMAJABCD
      EKL $.
      $( [16-Dec-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An equality theorem for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpteq1 $p |- ( A = B -> ( x e. A |-> C ) = ( x e. B |-> C ) ) $=
      ( wceq wral cmpt cv wcel eqidd rgen mpteq12 mpan2 ) BCEDDEZABFABDGACDGENA
      BAHBIDJKABDCDLM $.
      $( [16-Dec-2013] $)
  $}

  ${
    mpteq2ia.1 $e |- ( x e. A -> B = C ) $.
    $( An equality inference for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpteq2ia $p |- ( x e. A |-> B ) = ( x e. A |-> C ) $=
      ( wceq wal wral cmpt eqid ax-gen rgen mpteq12f mp2an ) BBFZAGCDFZABHABCIA
      BDIFOABJKPABELABCBDMN $.
      $( [16-Dec-2013] $)
  $}

  ${
    mpteq2i.1 $e |- B = C $.
    $( An equality inference for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpteq2i $p |- ( x e. A |-> B ) = ( x e. A |-> C ) $=
      ( wceq cv wcel a1i mpteq2ia ) ABCDCDFAGBHEIJ $.
      $( [16-Dec-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z D $.  $d x y z E $.  $d z C $.
    $d z F $.
    $( An equality theorem for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpt2eq123 $p |- ( ( A = D /\ B = E /\ A. x e. A A. y e. B C = F ) ->
                 ( x e. A , y e. B |-> C ) = ( x e. D , y e. E |-> F ) ) $=
      ( vz wceq wral cv wcel wa copab2 cmpt2 ax-17 hb3an eleq2 df-mpt2 hbra1 wb
      w3a hbra2 ra42 eqeq2d pm5.32da bi2anan9 anbi1d sylan9bbr oprabbid 3eqtr4g
      imp 3impa ) CFJZDGJZEHJZBDKZACKZUCZALZCMZBLZDMZNZILZEJZNZABIOVAFMZVCGMZNZ
      VFHJZNZABIOABCDEPABFGHPUTVHVMABIUOUPUSAUOAQUPAQURACUARUOUPUSBUOBQUPBQUQAB
      CDUDRUTIQUOUPUSVHVMUBUSVHVEVLNUOUPNZVMUSVEVGVLUSVENEHVFUSVEUQUQABCDUEUMUF
      UGVNVEVKVLUOVBVIUPVDVJCFVASDGVCSUHUIUJUNUKABICDETABIFGHTUL $.
      $( [16-Dec-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
    $( An equality theorem for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpt2eq12 $p |- ( ( A = C /\ B = D ) ->
                 ( x e. A , y e. B |-> E ) = ( x e. C , y e. D |-> E ) ) $=
      ( wceq wral cmpt2 cv wcel wa eqidd rgen2 mpt2eq123 mp3an3 ) CEHDFHGGHZBDI
      ACIABCDGJABEFGJHRABCDAKCLBKDLMGNOABCDGEFGPQ $.
      $( [16-Dec-2013] $)
  $}

  ${
    $d z A $.  $d z B $.  $d z C $.  $d z D $.  $d z E $.  $d z F $.
    $d x z ph $.  $d y z ph $.
    mpt2eq123dv.1 $e |- ( ph -> A = D ) $.
    mpt2eq123dv.2 $e |- ( ph -> B = E ) $.
    mpt2eq123dv.3 $e |- ( ph -> C = F ) $.
    $( An equality deduction for the maps to notation. $)
    mpt2eq123dv $p |- ( ph
            -> ( x e. A , y e. B |-> C ) = ( x e. D , y e. E |-> F ) ) $=
      ( vz cv wcel wa wceq copab2 cmpt2 eleq2d anbi12d eqeq2d oprabbidv df-mpt2
      3eqtr4g ) ABNZDOZCNZEOZPZMNZFQZPZBCMRUFGOZUHHOZPZUKIQZPZBCMRBCDEFSBCGHISA
      UMURBCMAUJUPULUQAUGUNUIUOADGUFJTAEHUHKTUAAFIUKLUBUAUCBCMDEFUDBCMGHIUDUE
      $.
      $( [12-Sep-2011] $)
  $}

  ${
    mpt2eq123i.1 $e |- A = D $.
    mpt2eq123i.2 $e |- B = E $.
    mpt2eq123i.3 $e |- C = F $.
    $( An equality inference for the maps to notation. $)
    mpt2eq123i $p |- ( x e. A , y e. B |-> C ) = ( x e. D , y e. E |-> F ) $=
      ( wtru cmpt2 wceq tru a1i mpt2eq123dv ax-mp ) LABCDEMABFGHMNOLABCDEFGHCFN
      LIPDGNLJPEHNLKPQR $.
      $( [15-Jul-2013] $)
  $}

  ${
    mpteq12i.1 $e |- A = C $.
    mpteq12i.2 $e |- B = D $.
    $( An equality inference for the maps to notation.  (Contributed by Scott
       Fenton, 27-Oct-2010.) $)
    mpteq12i $p |- ( x e. A |-> B ) = ( x e. C |-> D ) $=
      ( wtru cmpt wceq tru a1i mpteq12dv ax-mp ) HABCIADEIJKHABCDEBDJHFLCEJHGLM
      N $.
      $( [16-Dec-2013] $) $( [27-Oct-2010] $)
  $}

  ${
    mpteq2da.1 $e |- ( ph -> A. x ph ) $.
    mpteq2da.2 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Slightly more general equality inference for the maps to notation.
       (Contributed by FL, 14-Sep-2013.) $)
    mpteq2da $p |- ( ph -> ( x e. A |-> B ) = ( x e. A |-> C ) ) $=
      ( wceq wal wral cmpt eqid ax-gen cv wcel ex r19.21ai mpteq12f sylancr )
      ACCHZBIDEHZBCJBCDKBCEKHTBCLMAUABCFABNCOUAGPQBCDCERS $.
      $( [16-Dec-2013] $) $( [14-Sep-2013] $)
  $}

  ${
    $d x ph $.
    mpteq2dva.1 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Slightly more general equality inference for the maps to notation.
       (Contributed by Scott Fenton, 25-Apr-2012.) $)
    mpteq2dva $p |- ( ph -> ( x e. A |-> B ) = ( x e. A |-> C ) ) $=
      ( ax-17 mpteq2da ) ABCDEABGFH $.
      $( [25-Apr-2012] $)
  $}

  ${
    $d x z ph $.  $d y z ph $.  $d z A $.  $d z B $.  $d z C $.  $d z D $.
    mpt2eq3dva.1 $e |- ( ( ph /\ x e. A /\ y e. B ) -> C = D ) $.
    $( Slightly more general equality inference for the maps to notation. $)
    mpt2eq3dva $p |- ( ph -> ( x e. A , y e. B |-> C )
              = ( x e. A , y e. B |-> D ) ) $=
      ( vz cv wcel wa wceq copab2 cmpt2 3expb eqeq2d pm5.32da oprabbidv df-mpt2
      3eqtr4g ) ABJDKZCJEKZLZIJZFMZLZBCINUDUEGMZLZBCINBCDEFOBCDEGOAUGUIBCIAUDUF
      UHAUDLFGUEAUBUCFGMHPQRSBCIDEFTBCIDEGTUA $.
      $( [16-Dec-2013] $) $( [17-Oct-2013] $)
  $}

  ${
    mpt2eq3ia.1 $e |- ( ( x e. A /\ y e. B ) -> C = D ) $.
    $( An equality inference for the maps to notation.  (Contributed by Mario
       Carneiro, 16-Dec-2013.) $)
    mpt2eq3ia $p |- ( x e. A , y e. B |-> C ) = ( x e. A , y e. B |-> D ) $=
      ( wtru cmpt2 wceq tru cv wcel 3adant1 mpt2eq3dva ax-mp ) HABCDEIABCDFIJKH
      ABCDEFALCMBLDMEFJHGNOP $.
      $( [16-Dec-2013] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x y $.  $d w x z $.
    hbmpt.1 $e |- ( z e. A -> A. x z e. A ) $.
    hbmpt.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( Bound-variable hypothesis builder for the maps-to notation. $)
    hbmpt $p |- ( z e. ( y e. A |-> B ) -> A. x z e. ( y e. A |-> B ) ) $=
      ( vw cmpt cv wcel wceq wa copab df-mpt hblem hbeleq hban hbopab hbxfreq )
      ACBDEIBJDKZHJELZMZBHNBHDEOUCBHACUAUBAACBDFPAHEACHEGPQRST $.
      $( [20-Feb-2013] $)
  $}

  ${
    $d A z $.  $d B z $.  $d x y $.  $d x z $.
    $( Bound-variable hypothesis builder for the maps-to notation.
       (Contributed by FL, 17-Feb-2008.) $)
    hbmpt1 $p |- ( y e. ( x e. A |-> B ) -> A. x y e. ( x e. A |-> B ) ) $=
      ( vz cmpt cv wcel wceq wa copab df-mpt hbopab1 hbxfreq ) ABACDFAGCHEGDIJZ
      AEKAECDLOAEBMN $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d w A $.  $d w B $.  $d w C $.  $d w x $.  $d w y $.  $d x z $.
    $( Bound-variable hypothesis builder for an operation in maps-to
       notation. $)
    hbmpt21 $p |- ( z e. ( x e. A , y e. B |-> C )
         -> A. x z e. ( x e. A , y e. B |-> C ) ) $=
      ( vw cmpt2 cv wcel wa wceq copab2 df-mpt2 hboprab1 hbxfreq ) ACABDEFHAIDJ
      BIEJKGIFLKZABGMABGDEFNQABGCOP $.
      $( [27-Aug-2013] $)
  $}

  ${
    $d w A $.  $d w B $.  $d w C $.  $d w x $.  $d w y $.  $d y z $.
    $( Bound-variable hypothesis builder for an operation in maps-to
       notation. $)
    hbmpt22 $p |- ( z e. ( x e. A , y e. B |-> C )
         -> A. y z e. ( x e. A , y e. B |-> C ) ) $=
      ( vw cmpt2 cv wcel wa wceq copab2 df-mpt2 hboprab2 hbxfreq ) BCABDEFHAIDJ
      BIEJKGIFLKZABGMABGDEFNQABGCOP $.
      $( [27-Aug-2013] $)
  $}

  ${
    $d v w x z $.  $d v y z $.  $d v w A $.  $d v z B $.  $d v w C $.
    hbmpt2.1 $e |- ( w e. A -> A. z w e. A ) $.
    hbmpt2.2 $e |- ( w e. B -> A. z w e. B ) $.
    hbmpt2.3 $e |- ( w e. C -> A. z w e. C ) $.
    $( Bound-variable hypothesis builder for the maps-to notation. $)
    hbmpt2 $p |- ( w e. ( x e. A , y e. B |-> C ) ->
                  A. z w e. ( x e. A , y e. B |-> C ) ) $=
      ( vv cmpt2 cv wcel wa wceq copab2 df-mpt2 ax-17 hban hbel hboprab hbxfreq
      hbeq ) CDABEFGLAMZENZBMFNZOZKMZGPZOZABKQABKEFGRUKABKCDUHUJCUFUGCCDDUEEDMZ
      UENCSHUAUGCSTCDDUIGULUINCSJUDTUBUC $.
      $( [20-Feb-2013] $)
  $}

  ${
    $d v w x A $.  $d v w y A $.  $d v w z B $.  $d v w z C $.
    cbvmpt.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbvmpt.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbvmpt.3 $e |- ( x = y -> B = C ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  This version has bound-variable hypotheses in place of
       distinct variable conditions. $)
    cbvmpt $p |- ( x e. A |-> B ) = ( y e. A |-> C ) $=
      ( vw vv cv wcel wceq wa copab cmpt wsbc ax-17 hban eleq1 sbequ12 cbvopab1
      hbs1 anbi12d hblem hbeleq hbsb sbequ eqeq2d syl6bb eqtri df-mpt 3eqtr4i
      sbie ) ALZDMZJLZENZOZAJPZBLZDMZURFNZOZBJPZADEQBDFQVAKLZDMZUSAVGRZOZKJPVFU
      TVJAJKUTKSVHVIAVHASUSAKUDTUPVGNUQVHUSVIUPVGDUAUSAKUBUEUCVJVEKJBVHVIBVHBSU
      SAKBBJEBCJEGUFUGUHTVEKSVGVBNZVHVCVIVDVGVBDUAVKVIUSAVBRVDUSKBAUIUSVDABAJFA
      CJFHUFUGUPVBNEFURIUJUOUKUEUCULAJDEUMBJDFUMUN $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B y z $.  $d C x z $.
    cbvmptv.1 $e |- ( x = y -> B = C ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  (Contributed by Mario Carneiro, 19-Feb-2013.) $)
    cbvmptv $p |- ( x e. A |-> B ) = ( y e. A |-> C ) $=
      ( vz cv wcel ax-17 cbvmpt ) ABGCDEGHZDIBJLEIAJFK $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d v w x y z $.  $d u w x y z A $.  $d u w x y z B $.  $d u v C $.
    $d u v D $.
    cbvmpt2.1 $e |- ( v e. C -> A. z v e. C ) $.
    cbvmpt2.2 $e |- ( v e. C -> A. w v e. C ) $.
    cbvmpt2.3 $e |- ( v e. D -> A. x v e. D ) $.
    cbvmpt2.4 $e |- ( v e. D -> A. y v e. D ) $.
    cbvmpt2.5 $e |- ( ( x = z /\ y = w ) -> C = D ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution. $)
    cbvmpt2 $p |- ( x e. A , y e. B |-> C ) = ( z e. A , w e. B |-> D ) $=
      ( vu cv wcel wa wceq ax-17 copab2 cmpt2 hbeq hban bi2anan9 eqeq2d anbi12d
      eleq1 cbvoprab12 df-mpt2 3eqtr4i ) APZFQZBPZGQZRZOPZHSZRZABOUACPZFQZDPZGQ
      ZRZUQISZRZCDOUAABFGHUBCDFGIUBUSVFABOCDUPURCUPCTCEEUQHEPUQQZCTJUCUDUPURDUP
      DTDEEUQHVGDTKUCUDVDVEAVDATAEEUQIVGATLUCUDVDVEBVDBTBEEUQIVGBTMUCUDULUTSZUN
      VBSZRZUPVDURVEVHUMVAVIUOVCULUTFUHUNVBGUHUEVJHIUQNUFUGUIABOFGHUJCDOFGIUJUK
      $.
      $( [17-Dec-2013] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w z C $.  $d v x y D $.
    cbvmpt2v.1 $e |- ( x = z -> C = E ) $.
    cbvmpt2v.2 $e |- ( y = w -> E = D ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  With a longer proof analogous to ~ cbvmpt , some distinct
       variable requirements could be eliminated. $)
    cbvmpt2v $p |- ( x e. A , y e. B |-> C ) = ( z e. A , w e. B |-> D ) $=
      ( vv cv wcel ax-17 wceq sylan9eq cbvmpt2 ) ABCDLEFGHLMZGNZCOTDOSHNZAOUABO
      AMCMPBMDMPGIHJKQR $.
      $( [11-Jun-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set.  (Contributed by FL, 6-Jun-2011.) $)
    mptexg $p |- ( A e. V -> ( x e. A |-> B ) e. _V ) $=
      ( vy wcel cmpt cv wceq wa copab cvv df-mpt opabex2g syl5eqel ) BDFABCGAHB
      FEHCIJAEKLAEBCMAEBCDNO $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set. $)
    mpt2exg $p |- ( ( A e. V /\ B e. W )
                       -> ( x e. A , y e. B |-> C ) e. _V ) $=
      ( vz wcel wa cmpt2 cv wceq copab2 cvv df-mpt2 eqid oprabex2g syl5eqel )
      CFIDGIJABCDEKALCIBLDIJHLEMJABHNZOABHCDEPABHCDEFGTTQRS $.
      $( [12-Sep-2011] $)
  $}

  ${
    $d x y A $.
    mptex.1 $e |- A e. _V $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set.  (Contributed by Mario Carneiro, 20-Dec-2013.) $)
    mptex $p |- ( x e. A |-> B ) e. _V $=
      ( cvv wcel cmpt mptexg ax-mp ) BEFABCGEFDABCEHI $.
      $( [20-Dec-2013] $)

    $d x y B $.
    mpt2ex.2 $e |- B e. _V $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set.  (Contributed by Mario Carneiro, 20-Dec-2013.) $)
    mpt2ex $p |- ( x e. A , y e. B |-> C ) e. _V $=
      ( cvv wcel cmpt2 mpt2exg mp2an ) CHIDHIABCDEJHIFGABCDEHHKL $.
      $( [20-Dec-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Representation of a constant function using the mapping operation.
       (Note that ` x ` cannot appear free in ` B ` .) $)
    fconstmpt $p |- ( A X. { B } ) = ( x e. A |-> B ) $=
      ( vy csn cxp cv wcel wceq wa copab cmpt fconstopab df-mpt eqtr4i ) BCEFAG
      BHDGCIJADKABCLADBCMADBCNO $.
      $( [16-Nov-2013] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.
    dmmpt2.1 $e |- F = ( x e. A |-> B ) $.
    $( The domain of the mapping operation in general.  (Contributed by Mario
       Carneiro, 13-Sep-2013.) $)
    dmmpt2 $p |- dom F = { x e. A | B e. _V } $=
      ( vy cdm cv wcel wceq wa copab wex cab cvv crab cmpt df-mpt eqtri dmeqi
      dmopab 19.42v isset anbi2i bitr4i abbii df-rab eqtr4i 3eqtri ) DGAHBIZFHC
      JZKZAFLZGULFMZANZCOIZABPZDUMDABCQUMEAFBCRSTULAFUAUOUJUPKZANUQUNURAUNUJUKF
      MZKURUJUKFUBUPUSUJFCUCUDUEUFUPABUGUHUI $.
      $( [13-Sep-2013] $) $( [16-May-1995] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( The domain of the mapping operation is the stated domain, if the
       function value is always a set.  (Contributed by Mario Carneiro,
       9-Feb-2013.) $)
    dmmpt $p |- ( A. x e. A B e. V -> dom ( x e. A |-> B ) = A ) $=
      ( wcel wral cvv crab cmpt wceq elisset ralimi rabid2 sylibr eqid syl6reqr
      cdm dmmpt2 ) CDEZABFZBCGEZABHZABCIZQTUAABFBUBJSUAABCDKLUAABMNABCUCUCORP
      $.
      $( [14-Sep-2013] $) $( [9-Feb-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    rnmpt.1 $e |- F = ( x e. A |-> B ) $.
    $( The range of a function in maps-to notation.  (Contributed by Scott
       Fenton, 21-Mar-2011.) $)
    rnmpt $p |- ran F = { y | E. x e. A y = B } $=
      ( crn cv wcel wceq wa copab wrex cab cmpt df-mpt eqtri rneqi rnopab2 ) EG
      AHCIBHDJZKABLZGTACMBNEUAEACDOUAFABCDPQRABCDSQ $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    $( A function in maps-to notation is a function.  (Contributed by Mario
       Carneiro, 13-Jan-2013.) $)
    funmpt $p |- Fun ( x e. A |-> B ) $=
      ( vy cmpt wfun cv wcel wceq wa copab funopab4 df-mpt funeqi mpbir ) ABCEZ
      FAGBHZDGCIJADKZFQADCLPRADBCMNO $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.
    mptfng.1 $e |- F = ( x e. A |-> B ) $.
    $( The maps-to notation defines a function with domain.  (Contributed by
       Scott Fenton, 21-Mar-2011.) $)
    mptfng $p |- ( A. x e. A B e. _V <-> F Fn A ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fnopab2g ) AFBCDDABCGAHBIFHC
      JKAFLEAFBCMNO $.
      $( [21-Mar-2011] $)

    $( The maps-to notation defines a function with domain. $)
    fnmpt $p |- ( A. x e. A B e. V -> F Fn A ) $=
      ( wcel wral cvv wfn elisset ralimi mptfng sylib ) CEGZABHCIGZABHDBJOPABCE
      KLABCDFMN $.
      $( [9-Apr-2013] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d C x y $.  $d ph x $.
    fmptdOLD.1 $e |- ( ph -> ( x e. A -> B e. C ) ) $.
    $( Domain and range of the mapping operation; deduction form.  (Contributed
       by Mario Carneiro, 13-Jan-2013.) $)
    fmptdOLD $p |- ( ph -> ( x e. A |-> B ) : A --> C ) $=
      ( vy wcel wral cmpt wf r19.21aiv df-mpt fopab2 sylib ) ADEHZBCICEBCDJZKAP
      BCFLBGCEDQBGCDMNO $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C y $.
    fmpt.1 $e |- F = ( x e. A |-> C ) $.
    $( Functionality of the mapping operation.  (Contributed by Mario Carneiro,
       26-Jul-2013.) $)
    fmpt $p |- ( A. x e. A C e. B <-> F : A --> B ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fopab2 ) AGBCDEEABDHAIBJGIDK
      LAGMFAGBDNOP $.
      $( [26-Jul-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x ph $.
    fmptd.1 $e |- ( ph -> ( x e. A -> B e. C ) ) $.
    fmptd.2 $e |- F = ( x e. A |-> B ) $.
    $( Domain and co-domain of the mapping operation; deduction form.
       (Contributed by Mario Carneiro, 13-Jan-2013.) $)
    fmptd $p |- ( ph -> F : A --> C ) $=
      ( vy cmpt wf wcel wral r19.21aiv df-mpt fopab2 sylib feq1i sylibr ) ACEBC
      DJZKZCEFKADELZBCMUAAUBBCGNBICEDTBICDOPQCEFTHRS $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d x y A $.  $d y C $.  $d y F $.  $d x y ph $.
    fmpt2d.1 $e |- ( ph -> ( x e. A -> B e. V ) ) $.
    fmpt2d.2 $e |- F = ( x e. A |-> B ) $.
    fmpt2d.3 $e |- ( ph -> ( y e. A -> ( F ` y ) e. C ) ) $.
    $( Domain and co-domain of the mapping operation; deduction form. $)
    fmpt2d $p |- ( ph -> F : A --> C ) $=
      ( wfn crn wss wf wcel wral r19.21aiv fnmpt syl cfv fnfvrnss syl11anc df-f
      cv sylanbrc ) AGDLZGMFNZDFGOAEHPZBDQUGAUIBDIRBDEGHJSTZAUGCUEGUAFPZCDQUHUJ
      AUKCDKRCDFGUBUCDFGUDUF $.
      $( [9-Apr-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C y $.
    $( Restriction of the mapping operation.  (Contributed by Mario Carneiro,
       15-Jul-2013.) $)
    resmpt $p |- ( B C_ A -> ( ( x e. A |-> C ) |` B ) = ( x e. B |-> C ) ) $=
      ( vy cv wcel wceq wa copab cres cmpt resopab2 df-mpt reseq1 ax-mp 3eqtr4g
      wss ) CBRAFZBGEFDHZIAEJZCKZSCGTIAEJABDLZCKZACDLTAECBMUCUAHUDUBHAEBDNUCUAC
      OPAECDNQ $.
      $( [15-Jul-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d D x y z $.  $d E z $.
    $( Restriction of the mapping operation.  (Contributed by Mario Carneiro,
       17-Dec-2013.) $)
    resmpt2 $p |- ( ( C C_ A /\ D C_ B ) ->
                    ( ( x e. A , y e. B |-> E ) |` ( C X. D ) ) =
                      ( x e. C , y e. D |-> E ) ) $=
      ( vz wss wa cv wcel wceq copab2 cxp cres cmpt2 resoprab2 df-mpt2 reseq1
      ax-mp 3eqtr4g ) ECIFDIJAKZCLBKZDLJHKGMZJABHNZEFOZPZUCELUDFLJUEJABHNABCDGQ
      ZUGPZABEFGQUEABHCDEFRUIUFMUJUHMABHCDGSUIUFUGTUAABHEFGSUB $.
      $( [17-Dec-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.
    fvmptg.1 $e |- ( x = A -> B = C ) $.
    fvmptg.2 $e |- F = ( x e. D |-> B ) $.
    $( Value of a function given in maps-to notation.  Analogous to
       ~ fvopab4g . $)
    fvmptg $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fvopab4g ) AJBCDEFGHGAECKALE
      MJLCNOAJPIAJECQRS $.
      $( [4-Aug-2008] $) $( [2-Oct-2007] $)

    ${
      fvmpt.3 $e |- C e. _V $.
      $( Value of a function given in maps-to notation. $)
      fvmpt $p |- ( A e. D -> ( F ` A ) = C ) $=
        ( wcel cvv cfv wceq fvmptg mpan2 ) BEJDKJBFLDMIABCDEKFGHNO $.
        $( [17-Aug-2011] $)
    $}
  $}

  ${
    $d y B $.  $d x y C $.
    fvmpts.1 $e |- B e. _V $.
    fvmpts.2 $e |- F = ( x e. C |-> B ) $.
    $( Value of a function given in maps-to notation, using explicit class
       substitution. $)
    fvmpts $p |- ( A e. C -> ( F ` A ) = [_ A / x ]_ B ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fvopab4s ) AHBCDEFEADCIAJDKH
      JCLMAHNGAHDCOPQ $.
      $( [17-Jul-2013] $)
  $}


  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.  $d y ph $.
    fvmptd.1 $e |- ( ph -> A. x ph ) $.
    fvmptd.2 $e |- ( ( ph /\ x = A ) -> B = C ) $.
    fvmptd.3 $e |- ( ph -> F = ( x e. D |-> B ) ) $.
    $( Deduction version of ~ fvmpt . $)
    fvmptd $p |- ( ( ph /\ A e. D /\ C e. V ) -> ( F ` A ) = C ) $=
      ( vy wcel w3a cv wceq wal wa 19.21ai 3ad2ant1 copab cfv ax-17 2alimi cmpt
      wi ex syl df-mpt syl6eq 3simpc fvopab4t syl111anc ) ACFMZEHMZNBOZCPZDEPZU
      FZLQBQZGUPFMLODPRBLUAZPZBQZUNUORCGUBEPAUNUTUOAALQZBQUTAVDBIALUCSAUSBLAUQU
      RJUGUDUHTAUNVCUOAVBBIAGBFDUEVAKBLFDUIUJSTAUNUOUKBLCDEFGHULUM $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    fvmpt2.1 $e |- F = ( x e. A |-> B ) $.
    $( Value of a function given by the "maps to" notation.  Analogous to
       ~ fvopab2 .  (Contributed by FL, 21-Jun-2010.) $)
    fvmpt2 $p |- ( ( x e. A /\ B e. C ) -> ( F ` x ) = B ) $=
      ( vy cv wcel wa cfv wceq copab cmpt df-mpt eqtri fveq1i fvopab2 syl5eq )
      AHZBIZCDIJTEKTUAGHCLJAGMZKCTEUBEABCNUBFAGBCOPQAGBCDRS $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( Representation of a function in terms of its values.  (Contributed by
       FL, 14-Sep-2013.)  (Revised by Mario Carneiro, 16-Dec-2013.) $)
    dffn5v $p |- ( F Fn A <-> F = ( x e. A |-> ( F ` x ) ) ) $=
      ( vy wfn cv wcel cfv wceq wa copab cmpt dffn5 df-mpt eqeq2i bitr4i ) CBEC
      AFZBGDFQCHZIJADKZICABRLZIADBCMTSCADBRNOP $.
      $( [16-Dec-2013] $) $( [14-Sep-2013] $)

  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( Representation of a function in terms of its values.  (Contributed by
       Mario Carneiro, 23-Dec-2013.) $)
    fnopr2v $p |- ( F Fn ( A X. B ) <->
                    F = ( x e. A , y e. B |-> ( x F y ) ) ) $=
      ( vz cxp wfn cv wcel wa co wceq copab2 cmpt2 fnoprv df-mpt2 eqeq2i bitr4i
      ) ECDGHEAIZCJBIZDJKFITUAELZMKABFNZMEABCDUBOZMABFCDEPUDUCEABFCDUBQRS $.
      $( [23-Dec-2013] $)

  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y C $.  $d w z D $.
    mpt2mpt.1 $e |- ( z = <. x , y >. -> C = D ) $.
    $( Express a two-argument function as a one-argument function, or
       vice-versa.  (Contributed by Mario Carneiro, 17-Dec-2013.) $)
    mpt2mpt $p |- ( z e. ( A X. B ) |-> C ) = ( x e. A , y e. B |-> D ) $=
      ( vw cv cxp wcel wceq wa copab copab2 cmpt cmpt2 cop wex elxp anass bitri
      anbi1i 19.41vv eqeq2d anbi2d pm5.32i 2exbii 3bitr2i opabbii eqtr4i df-mpt
      dfoprab2 df-mpt2 3eqtr4i ) CJZDEKZLZIJZFMZNZCIOZAJZDLBJZELNZUTGMZNZABIPZC
      URFQABDEGRVCUQVDVESMZVHNZBTATZCIOVIVBVLCIVBVJVFNZBTATZVANVMVANZBTATVLUSVN
      VAABUQDEUAUDVMVAABUEVOVKABVOVJVFVANZNVKVJVFVAUBVJVPVHVJVAVGVFVJFGUTHUFUGU
      HUCUIUJUKVHABICUNULCIURFUMABIDEGUOUP $.
      $( [17-Dec-2013] $)
  $}

  ${
    $d A x y z $.  $d B y $.  $d C y $.  $d F y z $.  $d G y z $.
    fvmptex.1 $e |- F = ( x e. A |-> B ) $.
    fvmptex.2 $e |- G = ( x e. A |-> ( _I ` B ) ) $.
    $( Express a function ` F ` whose value ` B ` may not always be a set in
       terms of another function ` G ` for which sethood is guaranteed.  (Note
       that ` ( _I `` B ) ` is just shorthand for
       ` if ( B e. _V , B , (/) ) ` , and it is always a set by ~ fvex .)  Note
       also that these functions are not the same; wherever ` B ( C ) ` is not
       a set, ` C ` is not in the domain of ` F ` (so it evaluates to the empty
       set), but ` C ` is in the domain of ` G ` , and ` G ( C ) ` is defined
       to be the empty set.  (Contributed by Mario Carneiro, 14-Jul-2013.) $)
    fvmptex $p |- ( F ` C ) = ( G ` C ) $=
      ( vy vz wcel cfv wceq cv fveq2 cid cvv wn c0 cdm eqeq12d cmpt hbmpt1 hbfv
      ax-17 hbxfreq hbeq wa fvmpt2 fvex mpan2 fvi sylan9eq eqtr4d ex wex exbidv
      ibar isset copab cab df-mpt eqtri dmeqi dmopab abeq2i notbid biimpa ndmfv
      3bitr4g fvprc pm2.61d vtoclgaf vtoclga dmopabss eqsstri sseli con3i dmmpt
      syl a1i mprg eleq2i sylnbir pm2.61i ) DBKZDELZDFLZMZINZELZWJFLZMZWIIDBWJD
      MWKWGWLWHWJDEOWJDFOUAANZELZWNFLZMZWMAJWJBJNWJKAUEZAJJWKWLAJWJEAJEABCUBZGA
      JBCUCUFWRUDAJWJFAJFABCPLZUBZHAJBWTUCUFWRUDUGWNWJMWOWKWPWLWNWJEOWNWJFOUAWN
      BKZCQKZWQXBXCWQXBXCUHWOCWPABCQEGUIXBXCWPWTCXBWTQKZWPWTMCPUJZABWTQFHUIUKZC
      QULUMUNUOXBXCRZWQXBXGUHZWOSWPXHWNETZKZRZWOSMXBXGXKXBXCXJXBWJCMZIUPXBXLUHZ
      IUPZXCXJXBXLXMIXBXLURUQICUSXNAXIXIXMAIUTZTZXNAVAEXOEWSXOGAIBCVBVCVDZXMAIV
      EVCVFVJVGVHWNEVIVTXBXGWPWTSXFCPVKUMUNUOVLVMVNWFRZWGSWHXRDXIKZRWGSMXSWFXIB
      DXIXPBXQXLAIBVOVPVQVRDEVIVTWFDFTZKWHSMXTBDXTXATZBFXAHVDXDYABMABABWTQVSXDX
      BXEWAWBVCWCDFVIWDUNWE $.
      $( [14-Jul-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    ovmpt4g.3 $e |- F = ( x e. A , y e. B |-> C ) $.
    $( Value of a function given by the "maps to" notation.  Analogous to
       ~ oprabval4g .  (Contributed by Mario Carneiro, 17-Dec-2013.) $)
    ovmpt4g $p |- ( ( x e. A /\ y e. B /\ C e. V ) -> ( x F y ) = C ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval4g ) ABICDEGFFABC
      DEJAKCLBKDLMIKENMABIOHABICDEPQR $.
      $( [17-Dec-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d L x y z $.  $d D z $.
    $d R z $.  $d S x y z $.
    ovmpt2x.1 $e |- ( ( x = A /\ y = B ) -> R = S ) $.
    ovmpt2x.2 $e |- ( x = A -> D = L ) $.
    ovmpt2x.3 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( The value of an operation class abstraction.  Variant of ~ ovmpt2ga
       which does not require ` D ` and ` x ` to be distinct.  (Contributed by
       Jeff Madsen, 10-Jun-2010.)  (Revised by Mario Carneiro, 20-Dec-2013.) $)
    ovmpt2x $p |- ( ( A e. C /\ B e. L /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz wcel w3a wa wceq cv co 3simpa eqeq2d biimp3ar biantrud eleq1d eleq2d
      wb simpl eleq1 sylan9bb anbi12d 3adant3 bitr3d moani cmpt2 copab2 df-mpt2
      moeq eqtri oprabvaligg mpd ) CEPZDKPZHJPZQVCVDRZCDIUAHSVCVDVEUBATZEPZBTZF
      PZRZOTZGSZRZVFABOCDHIEKJVGCSZVIDSZVLHSZQZVKVNVFVRVMVKVOVPVMVQVOVPRZGHVLLU
      CUDUEVOVPVKVFUHVQVSVHVCVJVDVSVGCEVOVPUIUFVOVJVIKPVPVDVOFKVIMUGVIDKUJUKULU
      MUNVMVKOOGUSUOIABEFGUPVNABOUQNABOEFGURUTVAVB $.
      $( [20-Dec-2013] $) $( [10-Jun-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d z R $.
    $d x y z S $.
    ovmpt2ga.1 $e |- ( ( x = A /\ y = B ) -> R = S ) $.
    ovmpt2ga.2 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( Value of an operation given by a maps-to rule.  Equivalent to
       ~ oprabval2ag .  (Contributed by Mario Carneiro, 19-Dec-2013.) $)
    ovmpt2ga $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval2ag ) ABMCDEFGHIJ
      KIABEFGNAOEPBOFPQMOGRQABMSLABMEFGTUAUB $.
      $( [19-Dec-2013] $)

    ${
      ovmpt2a.4 $e |- S e. _V $.
      $( Value of an operation given by a maps-to rule.  Equivalent to
         ~ oprabval2ag . $)
      ovmpt2a $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
        ( wcel cvv co wceq ovmpt2ga mp3an3 ) CEMDFMHNMCDIOHPLABCDEFGHINJKQR $.
        $( [19-Dec-2013] $)
    $}
  $}

  ${
    $d x y z A $.  $d B y z $.  $d x C y z $.  $d x D y z $.  $d x G $.
    $d z R $.  $d y S z $.
    ovmpt2g.1 $e |- ( x = A -> R = G ) $.
    ovmpt2g.2 $e |- ( y = B -> G = S ) $.
    ovmpt2g.3 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( Value of an operation given by a maps-to rule.  Equivalent to
       ~ oprabval2g .  (Unnecessary distinct variable restrictions were removed
       by David Abernethy, 19-Jun-2012.) $)
    ovmpt2g $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval2g ) ABOCDEFGHIJK
      LMIABEFGPAQERBQFRSOQGTSABOUANABOEFGUBUCUD $.
      $( [24-Jul-2012] $) $( [2-Oct-2007] $)

    ${
      ovmpt2.4 $e |- S e. _V $.
      $( Value of an operation given by a maps-to rule.  Equivalent to
         ~ oprabval2 . $)
      ovmpt2 $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
        ( wcel cvv co wceq ovmpt2g mp3an3 ) CEODFOHPOCDIQHRNABCDEFGHIJPKLMST $.
        $( [12-Sep-2011] $)
    $}
  $}

  ${
    $d A y z $.  $d B z $.  $d C z $.  $d x y z $.
    rngop.1 $e |- F = ( x e. A , y e. B |-> C ) $.
    $( The range of an operation given by the "maps to" notation.  (Contributed
       by FL, 20-Jun-2011.) $)
    rngop $p |- ran F = { z | E. x e. A E. y e. B z = C } $=
      ( crn cv wcel wa wceq copab2 wex cab wrex cmpt2 df-mpt2 eqtri r2ex bicomi
      rneqi rnoprab abbii 3eqtri ) GIAJDKBJEKLCJFMZLZABCNZIUHBOAOZCPUGBEQADQZCP
      GUIGABDEFRUIHABCDEFSTUCUHABCUDUJUKCUKUJUGABDEUAUBUEUF $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d x y $.  $d y B $.
    $( Function with universal domain in maps-to notation. $)
    mptv $p |- ( x e. _V |-> B ) = { <. x , y >. | y = B } $=
      ( cvv cmpt cv wcel wceq wa copab df-mpt visset biantrur opabbii eqtr4i )
      ADCEAFDGZBFCHZIZABJQABJABDCKQRABPQALMNO $.
      $( [16-Aug-2013] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z C $.
    $( Operation with universal domain in maps-to notation. $)
    mpt2v $p |- ( x e. _V , y e. _V |-> C )
                     = { <. <. x , y >. , z >. | z = C } $=
      ( cvv cmpt2 cv wcel wa wceq copab2 visset pm3.2i biantrur oprabbii eqtr4i
      df-mpt2 ) ABEEDFAGEHZBGEHZIZCGDJZIZABCKUAABCKABCEEDQUAUBABCTUARSALBLMNOP
      $.
      $( [16-Aug-2013] $)
  $}

  ${
    $d A x y $.
    $( The restricted identity expressed with the "maps to" notation.
       (Contributed by FL, 25-Apr-2012.) $)
    mptresid $p |- ( x e. A |-> x ) = ( _I |` A ) $=
      ( vy cv cmpt wcel weq wa copab cid cres df-mpt opabresid eqtri ) ABADZEOB
      FCAGHACIJBKACBOLACBMN $.
      $( [25-Apr-2012] $)
  $}

  ${
    $d x y A $.  $d x y C $.  $d x y D $.  $d y B $.
    fvmptn.1 $e |- ( x = D -> B = C ) $.
    fvmptn.2 $e |- F = ( x e. A |-> B ) $.
    $( When the mapping operation would otherwise evaluate to a proper class,
       it instead evaluates to the empty set.  (Contributed by Mario Carneiro,
       9-Sep-2013.) $)
    fvmptn $p |- ( -. C e. _V -> ( F ` D ) = (/) ) $=
      ( vy wcel cvv wn cfv c0 wceq wa cmpt fveq1i cv copab wi cres resmpt ax-mp
      wss ssv eqtr4i fvres fvopabn syl5eq sylan9eq ex cdm df-mpt eqtri dmopabss
      mptv dmeqi eqsstri sseli con3i ndmfv syl a1d pm2.61i ) EBJZDKJLZEFMZNOZUA
      VFVGVIVFVGPVHEAKCQZBUBZMZNEFVKFABCQZVKHBKUEVKVMOBUFAKBCUCUDUGRVFVGVLEVJMZ
      NEBVJUHVGVNEISCOZAITZMNEVJVPAICUQRAIECDGUIUJUKUJULVFLZVIVGVQEFUMZJZLVIVSV
      FVRBEVRASBJVOPAITZUMBFVTFVMVTHAIBCUNUOURVOAIBUPUSUTVAEFVBVCVDVE $.
      $( [9-Sep-2013] $)
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d V w y z $.
    $( The cross product of two sets is a set.  Proposition 6.2 of
       [TakeutiZaring] p. 23.  This version is proven using Replacement; see
       ~ xpexg for a version that uses the Power Set axiom instead.
       (Contributed by Mario Carneiro, 20-May-2013.) $)
    xpexgALT $p |- ( ( A e. V /\ B e. W ) -> ( A X. B ) e. _V ) $=
      ( vy vx vw vz wcel wa cv cmpt crn cvv wceq wrex wex wfn wral syl cxp cuni
      cop opex a1i rgen eqid fnmpt ax-mp fnex mpan elex biantrud 19.42v syl6bbr
      rnexg rexbidv rexcom4 syl6bb eleq2 pm5.32ri rexbii bitr3i exbii r19.21aiv
      r19.42v wb a1d df-mpt elrnopabg anbi2d exbidv bitr4d rexcom elxp2 3bitr4i
      eluni 3bitr4g eqrdv adantr sylan uniexg 3syl eqeltrd ) ACIZBDIZJZABUAZEBF
      AFKZEKZUCZLZMZLZMZUBZNWEWHWPOWFWEGWHWPWEGKZWMIZEBPZWQHKZIZWTWOIZJZHQZWQWH
      IZWQWPIWEWSXAWTWMOZEBPZJZHQZXDWEWSWRXFJZEBPZHQZXIWEWSXJHQZEBPXLWEWRXMEBWE
      WRWRXFHQZJXMWEXNWRWEWMNIZXNWLARZWEXOWKNIZFASZXPXQFAXQWIAIWIWJUDUEUFZFAWKW
      LNWLUGUHUIXPWEJWLNIXOACWLUJWLNUPTUKZHWMNULTUMWRXFHUNUOUQXJEHBURUSXHXKHXHX
      AXFJZEBPXKXAXFEBVFYAXJEBXFXAWRWTWMWQUTVAVBVCVDUOWEXCXHHWEXBXGXAWEXOEBSZXB
      XGVGWEXOEBWEXOWJBIXTVHVEZEHBWMWTNWNEHBWMVIVJTVKVLVMWQWKOZEBPFAPYDFAPZEBPX
      EWSYDFEABVNFEWQABVOWRYEEBXRWRYEVGXSFGAWKWQNWLFGAWKVIVJUIVBVPHWQWOVQVRVSVT
      WGWNNIZWONIWPNIWEWNBRZWFYFWEYBYGYCEBWMWNNWNUGUHTBDWNUJWAWNNUPWONWBWCWD $.
      $( [20-May-2013] $) $( [14-Aug-1994] $)
  $}

