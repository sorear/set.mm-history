$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        "Maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |-> $.  $( "Maps to" symbol $)

  $( Extend the definition of a class to include "maps to" notation for
     defining a function via a rule. $)
  cmpt $a class ( x e. A |-> B ) $.

  $( Extend the definition of a class to include "maps to" notation for
     defining an operation via a rule. $)
  cmpt2 $a class ( x e. A , y e. B |-> C ) $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define "maps to" notation for defining a function via a rule.  Read as
       "the function defined by the map from ` x ` (in ` A ` ) to
       ` B ( x ) ` ."  The class expression ` B ` is the value of the function
       at ` x ` and normally contains the variable ` x ` .  An example is the
       square function for complex numbers, ` ( x e. CC |-> ( x ^ 2 ) ` .
       Similar to the definition of mapping in [ChoquetDD] p. 2. $)
    df-mpt $a |- ( x e. A |-> B ) =
                    { <. x , y >. | ( x e. A /\ y = B ) } $.

    $( Define "maps to" notation for defining an operation via a rule.  Read as
       "the operation defined by the map from ` x , y ` (in ` A X. B ` ) to
       ` B ( x , y ) ` ."  An extension of ~ df-mpt for two arguments. $)
    df-mpt2 $a |- ( x e. A , y e. B |-> C ) =
             { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.
    mptg.1 $e |- ( x = A -> B = C ) $.
    mptg.2 $e |- F = ( x e. D |-> B ) $.
    $( Value of a function given by a "maps to" rule.  Equivalent to
       ~ fvopab4g . $)
    mptg $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtr fvopab4g ) AJBCDEFGHGAECKALEM
      JLCNOAJPIAJECQRS $.
      $( [?] $) $( [2-Oct-2007] $)
  $}

  ${
    $d x y z A $.  $d x B y z $.  $d x C y z $.  $d x D y z $.  $d x G $.
    $d z R $.  $d y S z $.
    mpt2g.1 $e |- ( x = A -> R = G ) $.
    mpt2g.2 $e |- ( y = B -> G = S ) $.
    mpt2g.3 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( Value of an operation given by a "maps to" rule.  Equivalent to
       ~ oprabval2g . $)
    mpt2g $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtr oprabval2g ) ABOCDEFGHIJKL
      MIABEFGPAQERBQFRSOQGTSABOUANABOEFGUBUCUD $.
      $( [?] $) $( [2-Oct-2007] $)
  $}

