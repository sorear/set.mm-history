$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        "Maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |-> $.  $( Maps-to symbol $)

  $( Extend the definition of a class to include maps-to notation for defining
     a function via a rule. $)
  cmpt $a class ( x e. A |-> B ) $.

  $( Extend the definition of a class to include maps-to notation for defining
     an operation via a rule. $)
  cmpt2 $a class ( x e. A , y e. B |-> C ) $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define maps-to notation for defining a function via a rule.  Read as
       "the function defined by the map from ` x ` (in ` A ` ) to
       ` B ( x ) ` ."  The class expression ` B ` is the value of the function
       at ` x ` and normally contains the variable ` x ` .  An example is the
       square function for complex numbers, ` ( x e. CC |-> ( x ^ 2 ) ` .
       Similar to the definition of mapping in [ChoquetDD] p. 2. $)
    df-mpt $a |- ( x e. A |-> B ) =
                    { <. x , y >. | ( x e. A /\ y = B ) } $.
  $}

  ${
    $d x z $.  $d y z $.  $d z A $.  $d z B $.  $d z C $.
    $( Define maps-to notation for defining an operation via a rule.  Read as
       "the operation defined by the map from ` x , y ` (in ` A X. B ` ) to
       ` B ( x , y ) ` ."  An extension of ~ df-mpt for two arguments. $)
    df-mpt2 $a |- ( x e. A , y e. B |-> C ) =
             { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
  $}

  ${
    $d y A $.  $d y C $.  $d y B $.  $d y D $.  $d x y ph $.
    mpteq12dv.1 $e |- ( ph -> A = C ) $.
    mpteq12dv.2 $e |- ( ph -> B = D ) $.
    $( An equality inference for the maps to notation. $)
    mpteq12dv $p |- ( ph -> ( x e. A |-> B ) = ( x e. C |-> D ) ) $=
      ( vy cv wcel wceq wa copab cmpt eleq2d eqeq2d anbi12d opabbidv df-mpt
      3eqtr4g ) ABJZCKZIJZDLZMZBINUBEKZUDFLZMZBINBCDOBEFOAUFUIBIAUCUGUEUHACEUBG
      PADFUDHQRSBICDTBIEFTUA $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d z A $.  $d z B $.  $d z C $.  $d z D $.  $d z E $.  $d z F $.
    $d x y z ph $.
    mpt2eq123dv.1 $e |- ( ph -> A = D ) $.
    mpt2eq123dv.2 $e |- ( ph -> B = E ) $.
    mpt2eq123dv.3 $e |- ( ph -> C = F ) $.
    $( An equality inference for the maps to notation. $)
    mpt2eq123dv $p |- ( ph
            -> ( x e. A , y e. B |-> C ) = ( x e. D , y e. E |-> F ) ) $=
      ( vz cv wcel wa wceq copab2 cmpt2 eleq2d anbi12d eqeq2d oprabbidv
      df-mpt2 3eqtr4g ) ABNZDOZCNZEOZPZMNZFQZPZBCMRUFGOZUHHOZPZUKIQZPZBCMRBCDEF
      SBCGHISAUMURBCMAUJUPULUQAUGUNUIUOADGUFJTAEHUHKTUAAFIUKLUBUAUCBCMDEFUDBCMG
      HIUDUE $.
      $( [12-Sep-2011] $)
  $}

  ${
    $d A z $.  $d B z $.  $d x y $.  $d x z $.
    $( Bound-variable hypothesis builder for a function in maps-to notation.
       (Contributed by FL, 17-Feb-2008.) $)
    hbmpt1 $p |- ( y e. ( x e. A |-> B ) -> A. x y e. ( x e. A |-> B ) ) $=
      ( vz cmpt cv wcel wceq wa copab df-mpt hbopab1 hbxfr ) ABACDFAGCHEGDIJZAE
      KAECDLOAEBMN $.
      $( [9-Sep-2010] $) $( [17-Feb-2008] $)
  $}

  ${
    $d v w x A $.  $d v w y A $.  $d v w z B $.  $d v w z C $.
    cbvmpt.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbvmpt.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbvmpt.3 $e |- ( x = y -> B = C ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  This version has bound-variable hypotheses in place of
       distinct variable conditions. $)
    cbvmpt $p |- ( x e. A |-> B ) = ( y e. A |-> C ) $=
      ( vw vv cv wcel wceq wa copab cmpt wsbc ax-17 hbs1 hban eleq1 sbequ12
      anbi12d cbvopab1 hblem hbeleq hbsb sbequ eqeq2d sbie syl6bb eqtri df-mpt
      3eqtr4i ) ALZDMZJLZENZOZAJPZBLZDMZURFNZOZBJPZADEQBDFQVAKLZDMZUSAVGRZOZKJP
      VFUTVJAJKUTKSVHVIAVHASUSAKTUAUPVGNUQVHUSVIUPVGDUBUSAKUCUDUEVJVEKJBVHVIBVH
      BSUSAKBBJEBCJEGUFUGUHUAVEKSVGVBNZVHVCVIVDVGVBDUBVKVIUSAVBRVDUSKBAUIUSVDAB
      AJFACJFHUFUGUPVBNEFURIUJUKULUDUEUMAJDEUNBJDFUNUO $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set.  (Contributed by FL, 6-Jun-2011.) $)
    mptexg $p |- ( A e. C -> ( x e. A |-> B ) e. _V ) $=
      ( vy wcel cv wceq wa copab cvv cmpt opabex2g df-mpt syl5eqel ) BDFAGBFEGC
      HIAEJKABCLAEBCDMAEBCNO $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set. $)
    mpt2exg $p |- ( ( A e. R /\ B e. S )
                       -> ( x e. A , y e. B |-> C ) e. _V ) $=
      ( vz wcel wa cv wceq copab2 cvv cmpt2 eqid oprabex2g df-mpt2 syl5eqel )
      CFIDGIJAKCIBKDIJHKELJABHMZNABCDEOABHCDEFGTTPQABHCDERS $.
      $( [12-Sep-2011] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.
    fvmptg.1 $e |- ( x = A -> B = C ) $.
    fvmptg.2 $e |- F = ( x e. D |-> B ) $.
    $( Value of a function given by a maps-to rule.  Equivalent to
       ~ fvopab4g . $)
    fvmptg $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fvopab4g ) AJBCDEFGHGAECKALE
      MJLCNOAJPIAJECQRS $.
      $( [4-Aug-2008] $) $( [2-Oct-2007] $)

    ${
      fvmpt.3 $e |- C e. _V $.
      $( Value of a function given by a maps-to rule. $)
      fvmpt $p |- ( A e. D -> ( F ` A ) = C ) $=
        ( wcel cvv cfv wceq fvmptg mpan2 ) BEJDKJBFLDMIABCDEKFGHNO $.
        $( [17-Aug-2011] $)
    $}
  $}

  ${
    $d x y z A $.  $d x B y z $.  $d x C y z $.  $d x D y z $.  $d x G $.
    $d z R $.  $d y S z $.
    ovmpt2g.1 $e |- ( x = A -> R = G ) $.
    ovmpt2g.2 $e |- ( y = B -> G = S ) $.
    ovmpt2g.3 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( Value of an operation given by a maps-to rule.  Equivalent to
       ~ oprabval2g . $)
    ovmpt2g $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval2g ) ABOCDEFGHIJK
      LMIABEFGPAQERBQFRSOQGTSABOUANABOEFGUBUCUD $.
      $( [6-Aug-2008] $) $( [2-Oct-2007] $)

    ${
      ovmpt2.4 $e |- S e. _V $.
      $( Value of an operation given by a maps-to rule.  Equivalent to
         ~ oprabval2 . $)
      ovmpt2 $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
        ( wcel cvv co wceq ovmpt2g mp3an3 ) CEODFOHPOCDIQHRNABCDEFGHIJPKLMST $.
        $( [12-Sep-2011] $)
    $}
  $}

