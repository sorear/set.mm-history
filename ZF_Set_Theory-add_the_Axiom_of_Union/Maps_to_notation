$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        "Maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c |-> $.  $( Maps-to symbol $)

  $( Extend the definition of a class to include maps-to notation for defining
     a function via a rule. $)
  cmpt $a class ( x e. A |-> B ) $.

  $( Extend the definition of a class to include maps-to notation for defining
     an operation via a rule. $)
  cmpt2 $a class ( x e. A , y e. B |-> C ) $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define maps-to notation for defining a function via a rule.  Read as
       "the function defined by the map from ` x ` (in ` A ` ) to
       ` B ( x ) ` ."  The class expression ` B ` is the value of the function
       at ` x ` and normally contains the variable ` x ` .  An example is the
       square function for complex numbers, ` ( x e. CC |-> ( x ^ 2 ) ` .
       Similar to the definition of mapping in [ChoquetDD] p. 2. $)
    df-mpt $a |- ( x e. A |-> B ) =
                    { <. x , y >. | ( x e. A /\ y = B ) } $.
  $}

  ${
    $d x z $.  $d y z $.  $d z A $.  $d z B $.  $d z C $.
    $( Define maps-to notation for defining an operation via a rule.  Read as
       "the operation defined by the map from ` x , y ` (in ` A X. B ` ) to
       ` B ( x , y ) ` ."  An extension of ~ df-mpt for two arguments. $)
    df-mpt2 $a |- ( x e. A , y e. B |-> C ) =
             { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
  $}

  ${
    $d y A $.  $d y C $.  $d y B $.  $d y D $.  $d x y ph $.
    mpteq12dv.1 $e |- ( ph -> A = C ) $.
    mpteq12dv.2 $e |- ( ph -> B = D ) $.
    $( An equality inference for the maps to notation. $)
    mpteq12dv $p |- ( ph -> ( x e. A |-> B ) = ( x e. C |-> D ) ) $=
      ( vy cv wcel wceq wa copab cmpt eleq2d eqeq2d anbi12d opabbidv df-mpt
      3eqtr4g ) ABJZCKZIJZDLZMZBINUBEKZUDFLZMZBINBCDOBEFOAUFUIBIAUCUGUEUHACEUBG
      PADFUDHQRSBICDTBIEFTUA $.
      $( [24-Aug-2011] $)
  $}

  ${
    $d z A $.  $d z B $.  $d z C $.  $d z D $.  $d z E $.  $d z F $.
    $d x y z ph $.
    mpt2eq123dv.1 $e |- ( ph -> A = D ) $.
    mpt2eq123dv.2 $e |- ( ph -> B = E ) $.
    mpt2eq123dv.3 $e |- ( ph -> C = F ) $.
    $( An equality inference for the maps to notation. $)
    mpt2eq123dv $p |- ( ph
            -> ( x e. A , y e. B |-> C ) = ( x e. D , y e. E |-> F ) ) $=
      ( vz cv wcel wa wceq copab2 cmpt2 eleq2d anbi12d eqeq2d oprabbidv
      df-mpt2 3eqtr4g ) ABNZDOZCNZEOZPZMNZFQZPZBCMRUFGOZUHHOZPZUKIQZPZBCMRBCDEF
      SBCGHISAUMURBCMAUJUPULUQAUGUNUIUOADGUFJTAEHUHKTUAAFIUKLUBUAUCBCMDEFUDBCMG
      HIUDUE $.
      $( [12-Sep-2011] $)
  $}

  ${
    $d y A $.  $d y C $.  $d y B $.  $d y D $.  $d x y $.
    mpteqi.1 $e |- A = C $.
    mpteqi.2 $e |- B = D $.
    $( An equality inference for the maps to notation.  (Contributed by Scott
       Fenton, 27-Oct-2010.) $)
    mpteqi $p |- ( x e. A |-> B ) = ( x e. C |-> D ) $=
      ( vy cv wcel wceq wa copab cmpt eleq2i eqeq2i anbi12i opabbii df-mpt
      3eqtr4i ) AIZBJZHIZCKZLZAHMUADJZUCEKZLZAHMABCNADENUEUHAHUBUFUDUGBDUAFOCEU
      CGPQRAHBCSAHDEST $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d A y $.  $d B y $.  $d C y $.  $d ph x y $.
    mpteq2dv.1 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Slightly more general equality inference for the maps to notation.
       (Contributed by Scott Fenton, 25-Apr-2012.) $)
    mpteq2dv $p |- ( ph -> ( x e. A |-> B ) = ( x e. A |-> C ) ) $=
      ( vy cv wcel wceq wa copab cmpt eqeq2d pm5.32da opabbidv df-mpt 3eqtr4g
      ) ABHCIZGHZDJZKZBGLSTEJZKZBGLBCDMBCEMAUBUDBGASUAUCASKDETFNOPBGCDQBGCEQR
      $.
      $( [25-Apr-2012] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x y $.  $d w x z $.
    hbmpt.1 $e |- ( z e. A -> A. x z e. A ) $.
    hbmpt.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( Bound-variable hypothesis builder for the maps-to notation. $)
    hbmpt $p |- ( z e. ( y e. A |-> B ) -> A. x z e. ( y e. A |-> B ) ) $=
      ( vw cmpt cv wcel wceq wa copab df-mpt hblem hbeleq hban hbopab hbxfreq
      ) ACBDEIBJDKZHJELZMZBHNBHDEOUCBHACUAUBAACBDFPAHEACHEGPQRST $.
      $( [20-Feb-2013] $)
  $}

  ${
    $d A z $.  $d B z $.  $d x y $.  $d x z $.
    $( Bound-variable hypothesis builder for the maps-to notation.
       (Contributed by FL, 17-Feb-2008.) $)
    hbmpt1 $p |- ( y e. ( x e. A |-> B ) -> A. x y e. ( x e. A |-> B ) ) $=
      ( vz cmpt cv wcel wceq wa copab df-mpt hbopab1 hbxfreq ) ABACDFAGCHEGDIJZ
      AEKAECDLOAEBMN $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d v w x A $.  $d v w y A $.  $d v w z B $.  $d v w z C $.
    cbvmpt.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbvmpt.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbvmpt.3 $e |- ( x = y -> B = C ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  This version has bound-variable hypotheses in place of
       distinct variable conditions. $)
    cbvmpt $p |- ( x e. A |-> B ) = ( y e. A |-> C ) $=
      ( vw vv cv wcel wceq wa copab cmpt wsbc ax-17 hbs1 hban eleq1 sbequ12
      anbi12d cbvopab1 hblem hbeleq hbsb sbequ eqeq2d sbie syl6bb eqtri df-mpt
      3eqtr4i ) ALZDMZJLZENZOZAJPZBLZDMZURFNZOZBJPZADEQBDFQVAKLZDMZUSAVGRZOZKJP
      VFUTVJAJKUTKSVHVIAVHASUSAKTUAUPVGNUQVHUSVIUPVGDUBUSAKUCUDUEVJVEKJBVHVIBVH
      BSUSAKBBJEBCJEGUFUGUHUAVEKSVGVBNZVHVCVIVDVGVBDUBVKVIUSAVBRVDUSKBAUIUSVDAB
      AJFACJFHUFUGUPVBNEFURIUJUKULUDUEUMAJDEUNBJDFUNUO $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B y z $.  $d C x z $.
    cbvmptv.1 $e |- ( x = y -> B = C ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  (Contributed by Mario Carneiro, 19-Feb-2013.) $)
    cbvmptv $p |- ( x e. A |-> B ) = ( y e. A |-> C ) $=
      ( vz cv wcel ax-17 cbvmpt ) ABGCDEGHZDIBJLEIAJFK $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w z C $.  $d v x y D $.
    cbvmpt2v.1 $e |- ( x = z -> C = E ) $.
    cbvmpt2v.2 $e |- ( y = w -> E = D ) $.
    $( Rule to change the bound variable in a maps-to function, using implicit
       substitution.  With a longer proof analogous to ~ cbvmpt , some distinct
       variable requirements could be eliminated. $)
    cbvmpt2v $p |- ( x e. A , y e. B |-> C ) = ( z e. A , w e. B |-> D ) $=
      ( vv cv wcel wa wceq copab2 cmpt2 eleq1 bi2anan9 sylan9eq eqeq2d anbi12d
      cbvoprab12v df-mpt2 3eqtr4i ) AMZENZBMZFNZOZLMZGPZOZABLQCMZENZDMZFNZOZULH
      PZOZCDLQABEFGRCDEFHRUNVAABLCDUGUOPZUIUQPZOZUKUSUMUTVBUHUPVCUJURUGUOESUIUQ
      FSTVDGHULVBVCGIHJKUAUBUCUDABLEFGUECDLEFHUEUF $.
      $( [11-Jun-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set.  (Contributed by FL, 6-Jun-2011.) $)
    mptexg $p |- ( A e. V -> ( x e. A |-> B ) e. _V ) $=
      ( vy wcel cmpt cv wceq wa copab cvv df-mpt opabex2g syl5eqel ) BDFABCGAHB
      FEHCIJAEKLAEBCMAEBCDNO $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    $( If the domain of a function given by maps-to notation is a set, the
       function is a set. $)
    mpt2exg $p |- ( ( A e. V /\ B e. W )
                       -> ( x e. A , y e. B |-> C ) e. _V ) $=
      ( vz wcel wa cmpt2 cv wceq copab2 cvv df-mpt2 eqid oprabex2g syl5eqel )
      CFIDGIJABCDEKALCIBLDIJHLEMJABHNZOABHCDEPABHCDEFGTTQRS $.
      $( [12-Sep-2011] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( The domain of the mapping operation is the stated domain, if the
       function value is always a set.  (Contributed by Mario Carneiro,
       9-Feb-2013.) $)
    dmmpt $p |- ( A. x e. A B e. V -> dom ( x e. A |-> B ) = A ) $=
      ( vy wcel wral cmpt cdm cv wceq wa copab df-mpt dmeqi wex cab dmopab
      abid2 hbra1 ra4 elex syl6 ancld simpl impbid1 19.42v syl6bbr abbid
      syl5reqr syl5eq ) CDFZABGZABCHZIAJBFZEJCKZLZAEMZIZBUNURAEBCNOUMUSUQEPZAQZ
      BUQAERUMBUOAQVAABSUMUOUTAULABTUMUOUOUPEPZLZUTUMUOVCUMUOVBUMUOULVBULABUAEC
      DUBUCUDUOVBUEUFUOUPEUGUHUIUJUKUK $.
      $( [9-Feb-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    rnmpt.1 $e |- F = ( x e. A |-> B ) $.
    $( The range of the mapping operation.  (Contributed by Scott Fenton,
       21-Mar-2011.) $)
    rnmpt $p |- ran F = { y | E. x e. A y = B } $=
      ( crn cv wcel wceq wa copab wrex cab cmpt df-mpt eqtri rneqi rnopab2 ) EG
      AHCIBHDJZKABLZGTACMBNEUAEACDOUAFABCDPQRABCDSQ $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    $( The mapping operation is a function.  (Contributed by Mario Carneiro,
       13-Jan-2013.) $)
    funmpt $p |- Fun ( x e. A |-> B ) $=
      ( vy cmpt wfun cv wcel wceq wa copab funopab4 df-mpt funeqi mpbir ) ABCEZ
      FAGBHZDGCIJADKZFQADCLPRADBCMNO $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d A x $.
    fnmpt.1 $e |- F = ( x e. A |-> B ) $.
    $( Functionality and domain of the maps-to notation. $)
    fnmpt $p |- ( A. x e. A B e. V -> F Fn A ) $=
      ( wcel wral cmpt wfun cdm wceq wa wfn dmmpt funmpt jctil fneq1i df-fn
      bitri sylibr ) CEGABHZABCIZJZUCKBLZMZDBNZUBUEUDABCEOABCPQUGUCBNUFBDUCFRUC
      BSTUA $.
      $( [9-Apr-2013] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d C x y $.  $d ph x $.
    fmptdOLD.1 $e |- ( ph -> ( x e. A -> B e. C ) ) $.
    $( Domain and range of the mapping operation; deduction form.  (Contributed
       by Mario Carneiro, 13-Jan-2013.) $)
    fmptdOLD $p |- ( ph -> ( x e. A |-> B ) : A --> C ) $=
      ( vy wcel wral cmpt wf r19.21aiv df-mpt fopab2 sylib ) ADEHZBCICEBCDJZKAP
      BCFLBGCEDQBGCDMNO $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x ph $.
    fmptd.1 $e |- ( ph -> ( x e. A -> B e. C ) ) $.
    fmptd.2 $e |- F = ( x e. A |-> B ) $.
    $( Domain and co-domain of the mapping operation; deduction form.
       (Contributed by Mario Carneiro, 13-Jan-2013.) $)
    fmptd $p |- ( ph -> F : A --> C ) $=
      ( vy cmpt wf wcel wral r19.21aiv df-mpt fopab2 sylib feq1i sylibr ) ACEBC
      DJZKZCEFKADELZBCMUAAUBBCGNBICEDTBICDOPQCEFTHRS $.
      $( [13-Jan-2013] $)
  $}

  ${
    $d x y A $.  $d y C $.  $d y F $.  $d x y ph $.
    fmpt2d.1 $e |- ( ph -> ( x e. A -> B e. V ) ) $.
    fmpt2d.2 $e |- F = ( x e. A |-> B ) $.
    fmpt2d.3 $e |- ( ph -> ( y e. A -> ( F ` y ) e. C ) ) $.
    $( Domain and co-domain of the mapping operation; deduction form. $)
    fmpt2d $p |- ( ph -> F : A --> C ) $=
      ( wfn crn wss wf wcel wral r19.21aiv fnmpt syl cv cfv fnfvrnss syl11anc
      df-f sylanbrc ) AGDLZGMFNZDFGOAEHPZBDQUGAUIBDIRBDEGHJSTZAUGCUAGUBFPZCDQUH
      UJAUKCDKRCDFGUCUDDFGUEUF $.
      $( [9-Apr-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.
    fvmptg.1 $e |- ( x = A -> B = C ) $.
    fvmptg.2 $e |- F = ( x e. D |-> B ) $.
    $( Value of a function given by a maps-to notation.  Analogous to
       ~ fvopab4g . $)
    fvmptg $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fvopab4g ) AJBCDEFGHGAECKALE
      MJLCNOAJPIAJECQRS $.
      $( [4-Aug-2008] $) $( [2-Oct-2007] $)

    ${
      fvmpt.3 $e |- C e. _V $.
      $( Value of a function given by a maps-to notation. $)
      fvmpt $p |- ( A e. D -> ( F ` A ) = C ) $=
        ( wcel cvv cfv wceq fvmptg mpan2 ) BEJDKJBFLDMIABCDEKFGHNO $.
        $( [17-Aug-2011] $)
    $}
  $}

  ${
    $d x y A $.  $d y B $.  $d x C y $.  $d x D y $.  $d y ph $.
    fvmptd.1 $e |- ( ph -> A. x ph ) $.
    fvmptd.2 $e |- ( ( ph /\ x = A ) -> B = C ) $.
    fvmptd.3 $e |- ( ph -> F = ( x e. D |-> B ) ) $.
    $( Deduction version of ~ fvmpt . $)
    fvmptd $p |- ( ( ph /\ A e. D /\ C e. V ) -> ( F ` A ) = C ) $=
      ( vy wcel w3a cv wceq wi wal wa copab cfv ax-17 19.21ai ex 2alimi syl
      3ad2ant1 cmpt df-mpt syl6eq 3simpc fvopab4t syl111anc ) ACFMZEHMZNBOZCPZD
      EPZQZLRBRZGUPFMLODPSBLTZPZBRZUNUOSCGUAEPAUNUTUOAALRZBRUTAVDBIALUBUCAUSBLA
      UQURJUDUEUFUGAUNVCUOAVBBIAGBFDUHVAKBLFDUIUJUCUGAUNUOUKBLCDEFGHULUM $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    fvmpt2.1 $e |- F = ( x e. A |-> B ) $.
    $( Value of a function given by the "maps to" notation.  Analogous to
       ~ fvopab2 .  (Contributed by FL, 21-Jun-2010.) $)
    fvmpt2 $p |- ( ( x e. A /\ B e. C ) -> ( F ` x ) = B ) $=
      ( vy cv wcel wa cfv wceq copab cmpt df-mpt eqtri fveq1i fvopab2 syl5eq )
      AHZBIZCDIJTEKTUAGHCLJAGMZKCTEUBEABCNUBFAGBCOPQAGBCDRS $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d x y z A $.  $d B y z $.  $d x C y z $.  $d x D y z $.  $d x G $.
    $d z R $.  $d y S z $.
    ovmpt2g.1 $e |- ( x = A -> R = G ) $.
    ovmpt2g.2 $e |- ( y = B -> G = S ) $.
    ovmpt2g.3 $e |- F = ( x e. C , y e. D |-> R ) $.
    $( Value of an operation given by a maps-to rule.  Equivalent to
       ~ oprabval2g .  (Unnecessary distinct variable restrictions were removed
       by David Abernethy, 19-Jun-2012.) $)
    ovmpt2g $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vz cmpt2 cv wcel wa wceq copab2 df-mpt2 eqtri oprabval2g ) ABOCDEFGHIJK
      LMIABEFGPAQERBQFRSOQGTSABOUANABOEFGUBUCUD $.
      $( [24-Jul-2012] $) $( [2-Oct-2007] $)

    ${
      ovmpt2.4 $e |- S e. _V $.
      $( Value of an operation given by a maps-to rule.  Equivalent to
         ~ oprabval2 . $)
      ovmpt2 $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
        ( wcel cvv co wceq ovmpt2g mp3an3 ) CEODFOHPOCDIQHRNABCDEFGHIJPKLMST $.
        $( [12-Sep-2011] $)
    $}
  $}

  ${
    $d A w x y z $.  $d B w x y z $.  $d V w y z $.
    $( The cross product of two sets is a set.  Proposition 6.2 of
       [TakeutiZaring] p. 23.  This version is proven using Replacement; see
       ~ xpexg for a version that uses the Power Set axiom instead.
       (Contributed by Mario Carneiro, 20-May-2013.) $)
    xpexgALT $p |- ( ( A e. V /\ B e. W ) -> ( A X. B ) e. _V ) $=
      ( vy vx vw vz wcel wa cxp cv cop cmpt crn cuni cvv wceq wrex wex wfn
      wral opex a1i rgen eqid fnmpt ax-mp fnex rnexg syl mpan elex biantrud
      19.42v syl6bbr rexbidv rexcom4 syl6bb r19.42v eleq2 pm5.32ri rexbii
      bitr3i exbii wb a1d r19.21aiv df-mpt elrnopabg anbi2d exbidv bitr4d
      rexcom elxp2 3bitr4i eluni 3bitr4g eqrdv adantr sylan uniexg 3syl
      eqeltrd ) ACIZBDIZJZABKZEBFAFLZELZMZNZOZNZOZPZQWEWHWPRWFWEGWHWPWEGLZWMIZE
      BSZWQHLZIZWTWOIZJZHTZWQWHIZWQWPIWEWSXAWTWMRZEBSZJZHTZXDWEWSWRXFJZEBSZHTZX
      IWEWSXJHTZEBSXLWEWRXMEBWEWRWRXFHTZJXMWEXNWRWEWMQIZXNWLAUAZWEXOWKQIZFAUBZX
      PXQFAXQWIAIWIWJUCUDUEZFAWKWLQWLUFUGUHXPWEJWLQIXOACWLUIWLQUJUKULZHWMQUMUKU
      NWRXFHUOUPUQXJEHBURUSXHXKHXHXAXFJZEBSXKXAXFEBUTYAXJEBXFXAWRWTWMWQVAVBVCVD
      VEUPWEXCXHHWEXBXGXAWEXOEBUBZXBXGVFWEXOEBWEXOWJBIXTVGVHZEHBWMWTQWNEHBWMVIV
      JUKVKVLVMWQWKRZEBSFASYDFASZEBSXEWSYDFEABVNFEWQABVOWRYEEBXRWRYEVFXSFGAWKWQ
      QWLFGAWKVIVJUHVCVPHWQWOVQVRVSVTWGWNQIZWOQIWPQIWEWNBUAZWFYFWEYBYGYCEBWMWNQ
      WNUFUGUKBDWNUIWAWNQUJWOQWBWCWD $.
      $( [20-May-2013] $) $( [14-Aug-1994] $)
  $}

