$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Peano's postulates
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Zero is a natural number.  One of Peano's 5 postulates for arithmetic.
     Proposition 7.30(1) of [TakeutiZaring] p. 42.  Note:  Unlike most
     textbooks, our proofs of ~ peano1 through ~ peano5 do not use the Axiom of
     Infinity.  Unlike Takeuti and Zaring, they also do not use the Axiom of
     Regularity. $)
  peano1 $p |- (/) e. om $=
    ( com wlim c0 wcel limom 0ellim ax-mp ) ABCADEAFG $.
    $( [15-May-1994] $)

  $( The successor of any natural number is a natural number.  One of Peano's 5
     postulates for arithmetic.  Proposition 7.30(2) of [TakeutiZaring]
     p. 42. $)
  peano2 $p |- ( A e. om -> suc A e. om ) $=
    ( com wcel csuc peano2b biimpi ) ABCADBCAEF $.
    $( [3-Sep-2003] $)

  $( The successor of any natural number is not zero.  One of Peano's 5
     postulates for arithmetic.  Proposition 7.30(3) of [TakeutiZaring]
     p. 42. $)
  peano3 $p |- ( A e. om -> suc A =/= (/) ) $=
    ( csuc c0 wne com wcel nsuceq0 a1i ) ABCDAEFAGH $.
    $( [2-Jun-2007] $) $( [3-Sep-2003] $)

  $( Two natural numbers are equal iff their successors are equal, i.e. the
     successor function is one-to-one.  One of Peano's 5 postulates for
     arithmetic.  Proposition 7.30(4) of [TakeutiZaring] p. 43. $)
  peano4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A = suc B <-> A = B ) ) $=
    ( com wcel con0 csuc wceq wb nnon suc11 syl2an ) ACDAEDBEDAFBFGABGHBCDAIBIA
    BJK $.
    $( [3-Sep-2003] $)

  ${
    $d x y A $.
    $( The induction postulate: any class containing zero and closed under the
       successor operation contains all natural numbers.  One of Peano's 5
       postulates for arithmetic.  Proposition 7.30(5) of [TakeutiZaring]
       p. 43, except our proof does not require the Axiom of Infinity.  The
       more traditional statement of mathematical induction as a theorem
       schema, with a basis and an induction hypothesis, is derived from this
       theorem as theorem ~ findes . $)
    peano5 $p |- ( ( (/) e. A /\
                 A. x e. om ( x e. A -> suc x e. A ) ) -> om C_ A ) $=
      ( vy c0 wcel cv csuc wi com wral wa wceq wrex wn adantl eleq1 ax-17 exp32
      wss wne cdif eldifn eldifi elndif biimpcd necon3bd syl5com nnsuc syl11anc
      cin imp adantlr adantr hbra1 hban ra4 visset sucid mpbiri peano2b syl6bbr
      eleq2 minel neldif sylan2 syl6bi imp3a eleq1a com12 imim12d com13 rexlimd
      mpid syl5 sylan9 a1i imp41 mtand nrexdv word ordom difss mp3an12 necon1bi
      mpd tz7.5 syl ssdif0 sylibr ) DBEZAFZBEZWKGZBEZHZAIJZKZIBUAZDLZIBSWQWRCFZ
      UJDLZCWRMZNWSWQXACWRWQWTWREZKZXAWTBEZXCXENWQWTIBUBOXDXAKWTWMLZAIMZXEXDXGX
      AWJXCXGWPWJXCKWTIEZWTDTZXGXCXHWJWTIBUCZOWJXCXIWJDWREZNXCXIDBIUDXCXKWTDWTD
      LXCXKWTDWRPUEUFUGUKAWTUHUIULUMWJWPXCXAXGXEHZWPXCXAXLHHHWJWPXCXAXLWPXCXAKZ
      KXFXEAIWPXMAWOAIUNXMAQUOXEAQWPWKIEZWOXMXFXEHWOAIUPXFWOXMXEXFXMWLWNXEXFXCX
      AWLXCXHXFXAWLHZXJXFXHWKWTEZXOXFXPWKWMEWKAUQURWTWMWKVBUSXFXHXNXPXOHXFXHWMI
      EXNWTWMIPWKUTVAXNXPXAWLXPXAKXNWKWRENWLWKWTWRVCWKIBVDVERVFVMVNVGWNXFXEWMBW
      TVHVIVJVKVOVLRVPVQWEVRVSXBWRDIVTWRISWRDTXBWAIBWBCIWRWFWCWDWGIBWHWI $.
      $( [18-Feb-2004] $)
  $}

  ${
    $d x A $.
    $( A natural number is either 0 or a successor. $)
    nn0suc $p |- ( A e. om -> ( A = (/) \/ E. x e. om A = suc x ) ) $=
      ( com wcel c0 wceq cv csuc wrex wn wne df-ne nnsuc sylan2br ex orrd ) BCD
      ZBEFZBAGHFACIZQRJZSTQBEKSBELABMNOP $.
      $( [27-May-1998] $)
  $}

