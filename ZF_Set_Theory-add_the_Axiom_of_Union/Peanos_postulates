$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Peano's postulates
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Zero is a natural number.  One of Peano's 5 postulates for arithmetic.
     Proposition 7.30(1) of [TakeutiZaring] p. 42.  Note: Unlike most
     textbooks, our proofs of ~ peano1 through ~ peano5 do not use the Axiom
     of Infinity.  Unlike Takeuti and Zaring, they also do not use the Axiom
     of Regularity. $)
  peano1 $p |- (/) e. om $=
    ( com wlim c0 wcel limom 0ellim ax-mp ) ABCADEAFG $.
    $( [15-May-1994] $)

  $( The successor of any natural number is a natural number.  One of Peano's
     5 postulates for arithmetic.  Proposition 7.30(2) of [TakeutiZaring]
     p. 42. $)
  peano2 $p |- ( A e. om -> suc A e. om ) $=
    ( com wcel csuc peano2b biimp ) ABCADBCAEF $.
    $( [3-Sep-2003] $)

  $( The successor of any natural number is not zero.  One of Peano's 5
     postulates for arithmetic.  Proposition 7.30(3) of [TakeutiZaring]
     p. 42. $)
  peano3 $p |- ( A e. om -> suc A =/= (/) ) $=
    ( csuc c0 wne com wcel nsuceq0 a1i ) ABCDAEFAGH $.
    $( [2-Jun-2007] $) $( [3-Sep-2003] $)

  $( Two natural numbers are equal iff their successors are equal, i.e. the
     successor function is one-to-one.  One of Peano's 5 postulates for
     arithmetic.  Proposition 7.30(4) of [TakeutiZaring] p. 43. $)
  peano4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A = suc B <-> A = B ) ) $=
    ( con0 wcel csuc wceq wb com suc11 nnont syl2an ) ACDBCDAEBEFABFGAHDBHDABIA
    JBJK $.
    $( [3-Sep-2003] $)

  ${
    $d x y A $.
    $( The induction postulate: any class containing zero and closed under the
       successor operation contains all natural numbers.  One of Peano's 5
       postulates for arithmetic.  Proposition 7.30(5) of [TakeutiZaring]
       p. 43. $)
    peano5 $p |- ( ( (/) e. A /\
                 A. x e. om ( x e. A -> suc x e. A ) ) -> om (_ A ) $=
      ( vy c0 wcel cv csuc wi com wral wa cdif wceq wss cin wrex wn eldifn
      adantl wne nnsuc eldifi eleq1 biimpcd necon3bd elndif syl5com imp sylanc
      adantlr adantr hbra1 ax-17 hban ra4 visset sucid eleq2 mpbiri peano2b
      syl6bbr neldif minel sylan2 exp32 syl6bi mpid syl5 imp3a eleq1a com12
      imim12d com13 sylan9 r19.23ad a1i imp41 mpd ex mtod nrexdv word ordom
      difss tz7.5 mp3an12 necon1bi syl ssdif0 sylibr ) DBEZAFZBEZWLGZBEZHZAIJZK
      ZIBLZDMZIBNWRWSCFZODMZCWSPZQWTWRXBCWSWRXAWSEZKZXBXABEZXDXFQWRXAIBRSXEXBXF
      XEXBKXAWNMZAIPZXFXEXHXBWKXDXHWQXAIEZXADTZXHWKXDKAXAUAXDXIWKXAIBUBZSWKXDXJ
      XDDWSEZQXJWKXDXLXADXADMXDXLXADWSUCUDUEDBIUFUGUHUIUJUKWKWQXDXBXHXFHZWQXDXB
      XMHHHWKWQXDXBXMWQXDXBKZKXGXFAIWQXNAWPAIULXNAUMUNXFAUMWQWLIEZWPXNXGXFHWPAI
      UOXGWPXNXFXGXNWMWOXFXGXDXBWMXGXIXBWMHZXDXGXIWLXAEZXPXGXQWLWNEWLAUPUQXAWNW
      LURUSXGXIXOXQXPHXGXIWNIEXOXAWNIUCWLUTVAXOXQXBWMXOWLWSEQWMXQXBKWLIBVBWLXAW
      SVCVDVEVFVGXKVHVIWOXGXFWNBXAVJVKVLVMVNVOVEVPVQVRVSVTWAXCWSDIWBWSINWSDTXCW
      CIBWDCIWSWEWFWGWHIBWIWJ $.
      $( [2-Mar-2004] $) $( [18-Feb-2004] $)
  $}

  ${
    $d x A $.
    $( A natural number is either 0 or a successor. $)
    nn0suc $p |- ( A e. om -> ( A = (/) \/ E. x e. om A = suc x ) ) $=
      ( com wcel c0 wceq cv csuc wrex wn wne nnsuc df-ne sylan2br ex orrd ) BCD
      ZBEFZBAGHFACIZQRJZSQBEKSTABLBEMNOP $.
      $( [27-May-1998] $)
  $}

