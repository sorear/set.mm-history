$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Abian's "most fundamental" fixed point theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y v A $.  $d x y z w v u F $.  $d y v u G $.
    abianfp.1 $e |- A e. _V $.
    abianfp.2 $e |- G = rec ( { <. z , w >. | w = ( F ` z ) } , x ) $.
    $( Lemma for ~ abianfp .  We prove by transfinite induction that if ` F `
       has a fixed point ` x ` , then its iterates also equal ` x ` .  This
       lemma is used for the "trivial" direction of the main theorem. $)
    abianfplem $p |- ( v e. On -> ( ( F ` x ) = x -> ( G ` v ) = x ) ) $=
      ( vy vu cv cfv wceq c0 csuc fveq2 eqeq1d copab crdg fveq1i visset rdg0
      eqtri a1i con0 wcel wa cvv fvex ax-17 hbopab1 hbrdg hbxfr hbfv
      rdgsucopab mpan2 id sylan9eqr sylan9eq exp32 wlim wral wi ciun rdglim2a
      mpan iuneq2i 3eqtr4g adantr iuneq2 wne word cuni df-lim 3simp2bi
      iunconst syl eqtrd ex a1d tfinds2 ) DLZGMZALZNZOGMZWENZJLZGMZWENZWIPZGMZW
      ENZWEFMZWENZDJWCONWDWGWEWCOGQRWCWINWDWJWEWCWIGQRWCWLNWDWMWEWCWLGQRWHWPWGO
      CLBLZFMZNZBCSZWETZMWEOGXAIUAWEWTAUBUCUDUEWIUFUGZWPWKWNXBWPWKUHWMWJFMZWEXB
      XCUIUGWMXCNWJFUJBCKWEWIWRXCUIGKLZWEUGBUKZXDWIUGBUKZBKWJFXDFUGBUKBKWIGBKGX
      AIBKWEWTWSBCKULXEUMUNXFUOUOIWQWJFQUPUQWKWPXCWOWEWJWEFQWPURUSUTVAWCVBZWKJW
      CVCZWFVDWPXGXHWFXGXHUHWDJWCWJVEZWEXGWDXINXHXGWCXAMZJWCWIXAMZVEZWDXIWCUIUG
      XGXJXLNDUBJWEWCUIWTVFVGWCGXAIUAJWCWJXKWJXKNWIWCUGWIGXAIUAUEVHVIVJXHXGXIJW
      CWEVEZWEJWCWJWEVKXGWCOVLZXMWENXGWCVMXNWCWCVNNWCVOVPJWCWEVQVRUSVSVTWAWB $.
      $( [4-Sep-2004] $)

    $( No longer active:
       ~ http://www.math.ucdavis.edu/~~suh/abian/abian-themostfixed.html . $)
    $( "A most fundamental fixed point theorem" of Alexander Abian (1923-1999),
       apparently proved in 1998.  Let ` G `` 0 = x ` , ` G `` 1 = F `` x ` ,
       ` G `` 2 = F `` ( F `` x ) ` ,... be the iterates of ` F ` .  The
       theorem reads (using our variable names):  "Let ` F ` be a mapping from
       a set ` A ` into itself.  Then ` F ` has a fixed point if and only if:
       There exists an element ` x ` of ` A ` such that for every ordinal
       ` v ` , ` G `` v ` is an element of ` A ` , and if ` G `` v ` is not a
       fixed point of ` F ` then the ` G `` u ` 's are all distinct for every
       ordinal ` u e. v ` ."  See ~ df-rdg for the ` rec ` operation.  The
       proof's key idea is to assume that ` F ` does not have a fixed point,
       then use the Axiom of Replacement in the form of ~ f1dmex to derive that
       the class of all ordinal numbers exists, contradicting ~ onprc .  Our
       version of this theorem does not require the hypothesis that ` F ` be a
       mapping.  Reference:
       ~ http://us2.metamath.org:88/abian-themostfixed.html .  For an
       application of this theorem, see
       ~ http://groups.google.com/group/sci.stat.math/msg/1737ee1133c24aeb for
       its use in a proof of Tarski's fixed point theorem. $)
    abianfp $p |- ( E. x e. A ( F ` x ) = x <-> E. x e. A
           A. v e. On ( ( G ` v ) e. A /\ ( -. ( F ` ( G ` v ) ) = ( G ` v ) ->
                    A. u e. v -. ( G ` v ) = ( G ` u ) ) ) ) $=
      ( vy cv cfv wceq wrex wcel wn wral wi wa con0 abianfplem imp eleq1d
      biimprd fveq2 id eqeq12d biimprcd sylcom pm2.24d jctird ex com13
      r19.21adv reximia cvv onprc r19.26 notbid rcla4cv imim1d ralimdv ralim
      syl6 com12 wf1 f1dmex wf ccnv wfun wfn copab crdg rdgfnon fneq1i mpbir
      ffnfv biimpri mpan cres wss ssid tz7.48lem wb fnresdm ax-mp cnveqi funeq
      sylib anim12i df-f1 sylibr sylancl syld exp3a com23 sylbi mtoi a1i
      r19.23aiv cbvrexv dfrex2 bitr2i impbii ) ALZGMZXFNZAFOZDLZHMZFPZXKGMZXKNZ
      QZXKELHMNQEXJRZSZTZDUARZAFOZXHXSAFXFFPZXHXRDUAXJUAPZXHYAXRYBXHYAXRSYBXHTZ
      YAXLXQYCXLYAYCXKXFFYBXHXKXFNZABCDFGHIJUBZUCUDUEYCXNXPYBXHXNYBXHYDXNYEYDXN
      XHYDXMXGXKXFXKXFGUFYDUGUHUIUJUCUKULUMUNUOUPXTKLZGMZYFNZQZKFRZQZXIXSYKAFXS
      YKSYAXSYJUAUQPZURXSXLDUARZXQDUARZTYJYLSZXLXQDUAUSYMYNYOYMYJYNYLYMYJYNYLYM
      YJYNTZXPDUARZYLYPYMYQYJYNYMYQSZYJYNXLXPSZDUARYRYJXQYSDUAYJXLXOXPYIXOKXKFY
      FXKNZYHXNYTYGXMYFXKYFXKGUFYTUGUHUTVAVBVCXLXPDUAVDVEUCVFYMYQYLUAFHVGZFUQPY
      LYMYQTZUAFUQHVHUUBUAFHVIZHVJZVKZTUUAYMUUCYQUUEHUAVLZYMUUCUUFCLBLGMNBCVMZX
      FVNZUAVLXFUUGVOUAHUUHJVPVQZUUCUUFYMTDUAFHVRVSVTYQHUAWAZVJZVKZUUEUAUAWBYQU
      ULUAWCDEUAHUUIWDVTUUKUUDNUULUUEWEUUJHUUFUUJHNUUIUAHWFWGWHUUKUUDWIWGWJWKUA
      FHWLWMIWNUMWOWPWQUCWRWSWTXAXIYHKFOYKXHYHAKFXFYFNZXGYGXFYFXFYFGUFUUMUGUHXB
      YHKFXCXDWJXE $.
      $( [10-Jun-2009] $) $( [5-Sep-2004] $)
  $}


