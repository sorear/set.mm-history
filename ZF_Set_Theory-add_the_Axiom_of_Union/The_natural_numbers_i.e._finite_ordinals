$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural numbers (i.e. finite ordinals)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol. $)
  $c om $. $( Greek letter omega $)

  $( Extend class notation to include the class of natural numbers. $)
  com $a class om $.

  ${
    $d x y $.
    $( Define the class of natural numbers, which are all ordinal numbers that
       are less than every limit ordinal, i.e. all finite ordinals.  Our
       definition is a variant of the Definition of N of [BellMachover] p. 471.
       See ~ dfom2 for an alternate definition.  Later, when we assume the
       Axiom of Infinity, we show ` om ` is a set in ~ omex , and ` om ` can
       then be defined per ~ dfom3 (the smallest inductive set) and ~ dfom4 .

       _Note_:  the natural numbers ` om ` are a subset of the ordinal numbers
       ~ df-on .  They are completely different from the natural numbers ` NN `
       ( ~ df-n ) that are a subset of the complex numbers defined much later
       in our development, although the two sets have analogous properties and
       operations defined on them. $)
    df-om $a |- om = { x | ( Ord x /\ A. y ( Lim y -> x e. y ) ) } $.
  $}

  ${
    $d x z $.  $d y z $.
    $( An alternate definition of the set of natural numbers ` om ` .
       Definition 7.28 of [TakeutiZaring] p. 42, who use the symbol K_I for the
       inner class builder of non-limit ordinal numbers (see ~ nlimon ). $)
    dfom2 $p |- om = { x e. On | suc x (_ { y e. On | -. Lim y } } $=
      ( vz cv word wlim wcel wi wal wa cab con0 csuc wn crab wss com visset
      elon anbi1i wb onsssuc ontri1 bitr3d ancoms wceq limeq negbid elrab a1i
      imbi12d pm5.74da cvv limelon mpan pm4.71ri imbi1i impexp ibar imbi2d
      pm4.1 syl5bb pm5.74i 3bitr syl6rbbr pm3.27 suceloni onelon ex syl ancrd
      impbid2 imbi1d syl5bbr bitrd albidv dfss2 syl6bbr pm5.32i bitr3 abbii
      df-om df-rab 3eqtr4 ) ADZEZCDZFZWEWGGZHZCIZJZAKWELGZWEMZBDZFZNZBLOZPZJZAK
      QWSALOWLWTAWLWMWKJWTWMWFWKWEARSTWMWKWSWMWKWGWNGZWGWRGZHZCIWSWMWJXCCWMWJWG
      LGZXCHZXCWMXEXDWINZXDWHNZJZHZHZWJWMXDXCXIWMXDJZXAXFXBXHXDWMXAXFUAXDWMJWGW
      EPXAXFWGWEUBWGWEUCUDUEXBXHUAXKWQXGBWGLWOWGUFWPWHWOWGUGUHUIUJUKULWJXDWHJZW
      IHXDWJHXJWHXLWIWHXDWGUMGWHXDCRWGUMUNUOUPUQXDWHWIURXDWJXIXDXFXGHXIWJXDXGXH
      XFXDXGUSUTWHWIVAVBVCVDVEWMXDXAJZXBHXCXEWMXMXAXBWMXMXAXDXAVFWMXAXDWMWNLGZX
      AXDHWEVGXNXAXDWNWGVHVIVJVKVLVMXDXAXBURVNVOVPCWNWRVQVRVSVTWAACWBWSALWCWD
      $.
      $( [4-Nov-2004] $) $( [1-Nov-2004] $)
  $}

  ${
    $d A x y $.
    elom.1 $e |- A e. V $.
    $( Membership in omega.  The hypothesis can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 . $)
    elom $p |- ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) $=
      ( vy cv word wlim wcel wi wal wa com wceq ordeq eleq1 imbi2d albidv
      anbi12d df-om elab2 ) DEZFZAEZGZUAUCHZIZAJZKBFZUDBUCHZIZAJZKDBLCUABMZUBUH
      UGUKUABNULUFUJAULUEUIUDUABUCOPQRDAST $.
      $( [15-May-1994] $)
  $}

  ${
    $d A x y $.
    $( Membership in omega.  The antecedent can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 . $)
    elomg $p |- ( A e. B ->
                ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) ) $=
      ( vy cv com wcel word wlim wi wal wa eleq1 wceq ordeq imbi2d albidv
      anbi12d visset elom vtoclbg ) DEZFGUBHZAEZIZUBUDGZJZAKZLBFGBHZUEBUDGZJZAK
      ZLDBCUBBFMUBBNZUCUIUHULUBBOUMUGUKAUMUFUJUEUBBUDMPQRAUBDSTUA $.
      $( [25-Mar-1995] $)
  $}

  ${
    $d x y $.
    $( Omega is a subset of ` On ` . $)
    omsson $p |- om (_ On $=
      ( vx vy com con0 cv wcel word wlim wi wal visset elom pm3.26bi elon
      sylibr ssriv ) ACDAEZCFZQGZQDFRSBEZHQTFIBJBQAKZLMQUANOP $.
      $( [13-Jun-1994] $)
  $}

  ${
    $d x y A $.
    $( The class of natural numbers is a subclass of any (infinite) limit
       ordinal.  Exercise 1 of [TakeutiZaring] p. 44.  Remarkably, our proof
       does not require the Axiom of Infinity. $)
    limomss $p |- ( Lim A -> om (_ A ) $=
      ( vx vy word wlim com wss limord con0 wcel wceq wo wi ordeleqon wa cv
      wal limeq eleq2 imbi12d cla4gv visset elom pm3.27bi syl5 com23 imp ssrdv
      ex omsson sseq2 mpbiri a1d jaoi sylbi mpcom ) ADZAEZFAGZAHUQAIJZAIKZLURUS
      MZANUTVBVAUTURUSUTUROBFAUTURBPZFJZVCAJZMUTVDURVEUTCPZEZVCVFJZMZCQZURVEMZV
      DVIVKCAIVFAKVGURVHVEVFARVFAVCSTUAVDVCDVJCVCBUBUCUDUEUFUGUHUIVAUSURVAUSFIG
      UJAIFUKULUMUNUOUP $.
      $( [4-Nov-2003] $) $( [30-Oct-2003] $)
  $}

  $( A natural number is an ordinal number. $)
  nnont $p |- ( A e. om -> A e. On ) $=
    ( com con0 omsson sseli ) BCADE $.
    $( [27-Jun-1994] $)

  ${
    nnon.1 $e |- A e. om $.
    $( A natural number is an ordinal number. $)
    nnon $p |- A e. On $=
      ( com wcel con0 nnont ax-mp ) ACDAEDBAFG $.
      $( [27-Jun-1994] $)
  $}

  $( A natural number is ordinal. $)
  nnord $p |- ( A e. om -> Ord A ) $=
    ( com wcel con0 word nnont eloni syl ) ABCADCAEAFAGH $.
    $( [17-Oct-1995] $)

  ${
    $d x y z $.
    $( Omega is ordinal.  Theorem 7.32 of [TakeutiZaring] p. 43. $)
    ordom $p |- Ord om $=
      ( vy vx vz com wtr con0 wss word cv wcel wa wi wal dftr2 wlim ordelord
      nnord sylan ancoms trel exp3a com12 limord ordtr syl syl5 a2d 19.20dv
      visset elom pm3.27bi imp jca sylibr ax-gen mpgbir omsson ordon trssord
      mp3an ) DEZDFGFHDHVAAIZBIZJZVCDJZKZVBDJZLZBMAABDNVHBVFVBHZCIZOZVBVJJZLZCM
      ZKVGVFVIVNVEVDVIVCHZVDVIVEVCVBPVCQRSVDVEVNVDVKVCVJJZLZCMZVNVEVDVQVMCVDVKV
      PVLVDVJEZVPVLLZVKVSVDVTVSVDVPVLVJVBVCTUAUBVKVJHVSVJUCVJUDUEUFUGUHVEVOVRCV
      CBUIUJUKUFULUMCVBAUIUJUNUOUPUQURDFUSUT $.
      $( [18-Oct-1995] $)
  $}

  $( A member of a natural number is a natural number. $)
  elnn $p |- ( ( A e. B /\ B e. om ) -> A e. om ) $=
    ( com wtr wcel wa wi word ordom ordtr ax-mp trel ) CDZABEBCEFACEGCHMICJKCAB
    LK $.
    $( [21-Jun-1998] $)

  $( The class of natural numbers ` om ` is either an ordinal number (if we
     accept the Axiom of Infinity) or the proper class of all ordinals (if we
     deny the Axiom of Infinity).  Remark in [TakeutiZaring] p. 43. $)
  omon $p |- ( om e. On \/ om = On ) $=
    ( com word con0 wcel wceq wo ordom ordeleqon mpbi ) ABACDACEFGAHI $.
    $( [10-May-1998] $)

  ${
    $d x y A $.
    $( A natural number is not a limit ordinal. $)
    nnlim $p |- ( A e. om -> -. Lim A ) $=
      ( vx com wcel wlim word wn nnord ordirr syl cv wi wal wa elomg ibi
      pm3.27d wceq limeq eleq2 imbi12d cla4gv mpd mtod ) ACDZAEZAADZUEAFZUGGAHA
      IJUEBKZEZAUIDZLZBMZUFUGLZUEUHUMUEUHUMNBACOPQULUNBACUIARUJUFUKUGUIASUIAATU
      AUBUCUD $.
      $( [18-Oct-1995] $)

    $( The class of natural numbers is a subclass of the class of non-limit
       ordinal numbers.  Exercise 4 of [TakeutiZaring] p. 42. $)
    omssnlim $p |- om (_ { x e. On | -. Lim x } $=
      ( vy com cv wlim wn con0 crab wcel wa nnont nnlim jca wceq limeq negbid
      elrab sylibr ssriv ) BCADZEZFZAGHZBDZCIZUDGIZUDEZFZJUDUCIUEUFUHUDKUDLMUBU
      HAUDGTUDNUAUGTUDOPQRS $.
      $( [14-Nov-2004] $) $( [2-Nov-2004] $)
  $}

  $( Omega is a limit ordinal.  Theorem 2.8 of [BellMachover] p. 473.
     Our proof, however, does not require the Axiom of Infinity. $)
  limom $p |- Lim om $=
    ( vx com word wlim ordom con0 wcel wceq wo ordeleqon wn ordirr ax-mp cv wi
    wal wa elomg wss wb ordtri1 adantr ordsseleq biimpd nnlim a1i limeq con3d
    com12 jaod sylan9 sylbird a3d mpanl2 limord sylan ex pm2.43b 19.21aiv
    jctil syl5bir mt3i limon mpbiri jaoi sylbi ) BCZBDZEVGBFGZBFHZIVHBJVIVHVJVI
    VHBBGZVGVKKEBLMVIVKVGANZDZBVLGZOZAPZQVHKZABFRVQVPVGVQVOAVQVMVNVMVQVOVLCZVQV
    OVMVRVGVQVOEVRVGQZVQQZVNVMVTVNKZVLBSZVMKZVSWBWATVQVLBUAUBVSWBVLBGZVLBHZIZVQ
    WCVSWBWFVLBUCUDVQWDWCWEWDWCOVQVLUEUFWEVQWCWEVMVHWEVMVHVLBUGUDUHUIUJUKULUMUN
    VLUOUPUQURUSEUTVAVBVJVHFDVCBFUGVDVEVFM $.
    $( [26-Mar-1995] $)

  $( A class belongs to omega iff its successor does. $)
  peano2b $p |- ( A e. om <-> suc A e. om ) $=
    ( com wlim wcel csuc wb limom limsuc ax-mp ) BCABDAEBDFGBAHI $.
    $( [3-Dec-1995] $)

  ${
    $d x y A $.
    $( A non-zero natural number is a successor. $)
    nnsuc $p |- ( ( A e. om /\ A =/= (/) ) -> E. x e. om A = suc x ) $=
      ( com wcel c0 wne wa cv csuc wceq con0 wrex wlim wn nnlim adantr word wi
      cuni wb orduninsuc w3a df-lim biimpr 3expia sylbird nnord sylan mt3d wex
      eleq1 biimpcd peano2b syl6ibr ancrd adantld 19.22dv df-rex 3imtr4g mpd )
      BCDZBEFZGZBAHZIZJZAKLZVFACLZVCVGBMZVAVINVBBOPBQZVBVGNZVIRVAVJVBGVKBBSJZVI
      VJVLVKTVBABUAPVJVBVLVIVIVJVBVLUBBUCUDUEUFBUGUHUIVAVGVHRVBVAVDKDZVFGZAUJVD
      CDZVFGZAUJVGVHVAVNVPAVAVFVPVMVAVFVOVAVFVECDZVOVFVAVQBVECUKULVDUMUNUOUPUQV
      FAKURVFACURUSPUT $.
      $( [6-Apr-2007] $) $( [18-Feb-2004] $)
  $}

