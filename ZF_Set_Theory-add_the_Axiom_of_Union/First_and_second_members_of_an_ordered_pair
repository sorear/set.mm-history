$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        First and second members of an ordered pair
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c 1st $. $( First member of an ordered pair $)
  $c 2nd $. $( Second member of an ordered pair $)

  $( Extend the definition of a class to include the first member an
     ordered pair function. $)
  c1st $a class 1st $.

  $( Extend the definition of a class to include the second member an
     ordered pair function. $)
  c2nd $a class 2nd $.

  ${
    $d x y z $.
    $( Define a function that extracts the first member of an ordered pair.
       Theorem ~ op1st proves that it does this.  Equivalent to Definition 5.13
       (i) of [Monk1] p. 52 (compare ~ op1sta and ~ op1stb ). The notation is
       the same as Monk's. $)
    df-1st $a |- 1st = { <. x , y >. | y = U. dom { x } } $.

    $( Define a function that extracts the second member of an ordered pair.
       Theorem ~ op2nd proves that it does this.  Equivalent to Definition 5.13
       (ii) of [Monk1] p. 52 (compare ~ op2nda and ~ op2ndb ). The notation is
       the same as Monk's. $)
    df-2nd $a |- 2nd = { <. x , y >. | y = U. ran { x } } $.
  $}

  ${
    $d x y A $. $d x y B $.
    $( The value of the function that extracts the first member of an ordered
       pair. $)
    1stval $p |- ( 1st ` A ) = U. dom { A } $=
      ( vy vx cvv wcel c1st cfv csn cdm cuni wceq snex dmexg ax-mp uniex wa cv
      copab sneq dmeqd unieqd fvopabg df-1st fveq1i syl5eq mpan2 wn c0 fvprc
      snprc biimp dm0 syl6eq uni0 eqtr4d pm2.61i ) ADEZAFGZAHZIZJZKZUQVADEZVBUT
      USDEUTDEALUSDMNOUQVCPABQCQZHZIZJZKCBRZGVAURCBAVGVADDVDAKZVFUTVIVEUSVDASTU
      AUBAFVHCBUCUDUEUFUQUGZURUHVAAFUIVJVAUHJUHVJUTUHVJUTUHIUHVJUSUHVJUSUHKAUJU
      KTULUMUAUNUMUOUP $.
      $( [14-Oct-2004] $) $( [9-Oct-2004] $)

    $( The value of the function that extracts the second member of an ordered
       pair. $)
    2ndval $p |- ( 2nd ` A ) = U. ran { A } $=
      ( vy vx cvv wcel c2nd cfv csn crn cuni wceq snex rnexg ax-mp uniex wa cv
      copab sneq rneqd unieqd fvopabg df-2nd fveq1i syl5eq mpan2 wn c0 fvprc
      snprc biimp rn0 syl6eq uni0 eqtr4d pm2.61i ) ADEZAFGZAHZIZJZKZUQVADEZVBUT
      USDEUTDEALUSDMNOUQVCPABQCQZHZIZJZKCBRZGVAURCBAVGVADDVDAKZVFUTVIVEUSVDASTU
      AUBAFVHCBUCUDUEUFUQUGZURUHVAAFUIVJVAUHJUHVJUTUHVJUTUHIUHVJUSUHVJUSUHKAUJU
      KTULUMUAUNUMUOUP $.
      $( [14-Oct-2004] $) $( [9-Oct-2004] $)
  $}

  $( The value of the first-member function at the empty set. $)
  1st0 $p |- ( 1st ` (/) ) = (/) $=
    ( c0 c1st cfv csn cdm cuni 1stval dmsn0 unieqi uni0 3eqtr ) ABCADEZFAFAAGLA
    HIJK $.
    $( [26-Apr-2007] $) $( [23-Apr-2007] $)

  $( The value of the second-member function at the empty set. $)
  2nd0 $p |- ( 2nd ` (/) ) = (/) $=
    ( c0 c2nd cfv csn crn cuni 2ndval cdm wceq dmsn0 dm0rn0 mpbi unieqi uni0
    3eqtr ) ABCADZEZFAFAAGQAPHAIQAIJPKLMNO $.
    $( [?] $) $( [23-Apr-2007] $)

  ${
    $d x y z A $. $d x y z B $.
    op1st.1 $e |- A e. V $.
    $( Extract the first member of an ordered pair. $)
    op1st $p |- ( 1st ` <. A , B >. ) = A $=
      ( cop c1st cfv csn cdm cuni 1stval op1sta eqtr ) ABDZEFMGHIAMJABCKL $.
      $( [16-Oct-2004] $) $( [5-Oct-2004] $)

    ${
      op2n.2 $e |- B e. V $.
      $( Extract the second member of an ordered pair. $)
      op2nd $p |- ( 2nd ` <. A , B >. ) = B $=
        ( cop c2nd cfv csn crn cuni 2ndval op2nda eqtr ) ABEZFGNHIJBNKABCDLM $.
        $( [16-Oct-2004] $) $( [5-Oct-2004] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Extract the first member of an ordered pair. $)
    op1stg $p |- ( A e. C -> ( 1st ` <. A , B >. ) = A ) $=
      ( vx cv cop c1st cfv wceq opeq1 fveq2d id eqeq12d visset op1st vtoclg )
      DEZBFZGHZQIABFZGHZAIDACQAIZSUAQAUBRTGQABJKUBLMQBDNOP $.
      $( [20-Jul-2005] $) $( [19-Jul-2005] $)

    $( Extract the second member of an ordered pair. $)
    op2ndg $p |- ( ( A e. C /\ B e. D ) -> ( 2nd ` <. A , B >. ) = B ) $=
      ( vx vy cv cop c2nd cfv wceq opeq1 fveq2d eqeq1d opeq2 id eqeq12d visset
      op2nd vtocl2g ) EGZFGZHZIJZUBKAUBHZIJZUBKABHZIJZBKEFABCDUAAKZUDUFUBUIUCUE
      IUAAUBLMNUBBKZUFUHUBBUJUEUGIUBBAOMUJPQUAUBERFRST $.
      $( [24-Jul-2005] $) $( [19-Jul-2005] $)

    $( Alternate value of the function that extracts the first member of an
       ordered pair.  Definition 5.13 (i) of [Monk1] p. 52. $)
    1stval2 $p |- ( A e. ( V X. V ) -> ( 1st ` A ) = |^| |^| A ) $=
      ( vx vy cvv cxp wcel cv cop wceq wex c1st cfv cint elvv visset op1st
      op1stb eqtr4 fveq2 inteq inteqd 3eqtr4a 19.23aivv sylbi ) ADDEFABGZCGZHZI
      ZCJBJAKLZAMZMZIZBCANUHULBCUHUGKLZUGMZMZUIUKUMUEUOUEUFBOZPUEUFUPQRAUGKSUHU
      JUNAUGTUAUBUCUD $.
      $( [14-Oct-2006] $) $( [18-Aug-2006] $)

    $( Alternate value of the function that extracts the second member of an
       ordered pair.  Definition 5.13 (ii) of [Monk1] p. 52. $)
    2ndval2 $p |- ( A e. ( V X. V ) -> ( 2nd ` A ) = |^| |^| |^| `' { A } ) $=
      ( vx vy cvv cxp wcel cv cop wceq wex c2nd cfv csn ccnv cint elvv visset
      op2nd op2ndb eqtr4 fveq2 sneq cnveq syl inteqd 3eqtr4a 19.23aivv sylbi )
      ADDEFABGZCGZHZIZCJBJAKLZAMZNZOZOZOZIZBCAPULUSBCULUKKLZUKMZNZOZOZOZUMURUTU
      JVEUIUJBQZCQZRUIUJVFVGSTAUKKUAULUQVDULUPVCULUOVBULUNVAIUOVBIAUKUBUNVAUCUD
      UEUEUEUFUGUH $.
      $( [18-Aug-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d x y z w v A $.  $d x y z B $.  $d x y z w v u $.
    $( The ` 1st ` function maps the universe onto the universe. $)
    fo1st $p |- 1st : V -onto-> V $=
      ( vy vx cvv c1st wfo cv csn cdm cuni wceq copab wfn crn df-fo wcel snex
      dmexg ax-mp uniex wa visset biantrur opabbii fnopab2 wex cab cop op1sta
      eqcomi opex sneq dmeqd unieqd eqeq2d cla4ev equid 2th abbii rnopab df-v
      3eqtr4 mpbir2an wb df-1st foeq1 mpbir ) CCDEZCCAFZBFZGZHZIZJZBAKZEZVOVNCL
      VNMZCJCCVNNBACVLVNVKVJCOVKCOVIPVJCQRSVMVICOZVMTBAVQVMBUAUBUCUDVMBUEZAUFVH
      VHJZAUFVPCVRVSAVRVSVHVHVHUGZGZHZIZJZVRWCVHVHVHAUAUHUIVMWDBVTVHVHUJVIVTJZV
      LWCVHWEVKWBWEVJWAVIVTUKULUMUNUORAUPUQURVMBAUSAUTVAVBDVNJVGVOVCBAVDCCDVNVE
      RVF $.
      $( [16-Oct-2004] $) $( [14-Oct-2004] $)

    $d x y z w v A $.  $d x y z B $.  $d x y z w v u $.
    $( The ` 2nd ` function maps the universe onto the universe. $)
    fo2nd $p |- 2nd : V -onto-> V $=
      ( vy vx cvv c2nd wfo cv csn crn cuni wceq copab wfn df-fo wcel snex
      rnexg ax-mp uniex wa visset biantrur opabbii fnopab2 wex cab cop op2nda
      eqcomi opex sneq rneqd unieqd eqeq2d cla4ev equid 2th abbii rnopab df-v
      3eqtr4 mpbir2an wb df-2nd foeq1 mpbir ) CCDEZCCAFZBFZGZHZIZJZBAKZEZVNVMCL
      VMHZCJCCVMMBACVKVMVJVICNVJCNVHOVICPQRVLVHCNZVLSBAVPVLBTUAUBUCVLBUDZAUEVGV
      GJZAUEVOCVQVRAVQVRVGVGVGUFZGZHZIZJZVQWBVGVGVGATZWDUGUHVLWCBVSVGVGUIVHVSJZ
      VKWBVGWEVJWAWEVIVTVHVSUJUKULUMUNQAUOUPUQVLBAURAUSUTVADVMJVFVNVBBAVCCCDVMV
      DQVE $.
      $( [16-Oct-2004] $) $( [14-Oct-2004] $)

    $( Mapping of a restriction of the ` 1st ` (first member of an ordered
       pair) function. $)
    f1stres $p |- ( 1st |` ( A X. B ) ) : ( A X. B ) --> A $=
      ( vx vy vz cv csn cdm cuni wcel cxp wral c1st cres wf cop visset op1sta
      eleq1i biimpr adantr rgen2a wceq sneq dmeqd unieqd eleq1d ralxp mpbir
      copab wa df-1st reseq1 ax-mp resopab eqtr fopab2 mpbi ) CFZGZHZIZAJZCABKZ
      LZVDAMVDNZOVEDFZEFZPZGZHZIZAJZEBLDALVMDEABVGAJZVMVHBJVMVNVLVGAVGVHDQRSTUA
      UBVCVMCDEABUSVIUCZVBVLAVOVAVKVOUTVJUSVIUDUEUFUGUHUICDVDAVBVFVFVGVBUCZCDUJ
      ZVDNZUSVDJVPUKCDUJMVQUCVFVRUCCDULMVQVDUMUNVPCDVDUOUPUQUR $.
      $( [16-Oct-2004] $) $( [11-Oct-2004] $)

    $( Mapping of a restriction of the ` 2nd ` (second member of an ordered
       pair) function. $)
    f2ndres $p |- ( 2nd |` ( A X. B ) ) : ( A X. B ) --> B $=
      ( vx vy vz cv csn crn cuni wcel cxp wral c2nd cres wf cop visset op2nda
      eleq1i biimpr adantl rgen2a wceq sneq rneqd unieqd eleq1d ralxp mpbir
      copab wa df-2nd reseq1 ax-mp resopab eqtr fopab2 mpbi ) CFZGZHZIZBJZCABKZ
      LZVDBMVDNZOVEDFZEFZPZGZHZIZBJZEBLDALVMDEABVHBJZVMVGAJVMVNVLVHBVGVHDQEQRST
      UAUBVCVMCDEABUSVIUCZVBVLBVOVAVKVOUTVJUSVIUDUEUFUGUHUICDVDBVBVFVFVGVBUCZCD
      UJZVDNZUSVDJVPUKCDUJMVQUCVFVRUCCDULMVQVDUMUNVPCDVDUOUPUQUR $.
      $( [10-Mar-2007] $) $( [7-Aug-2006] $)

    $( Value of an alternate definition of the ` 1st ` function. $)
    1st2val $p |- ( { <. <. x , y >. , z >. | z = x } ` A ) = ( 1st ` A ) $=
      ( vw vv cv cop wceq wex copab2 cfv c1st co visset op1st cvv wcel id eqid
      a1i oprabval5 mp2an df-opr 3eqtr2r fveq2 eqeq12d mpbii 19.23aivv wn c0
      cdm cxp wa copab pm3.2i a9e 2th opabbii df-xp dmoprab 3eqtr4r eleq2i
      elvv eqcom 2exbii 3bitr negbii ndmfv sylbir csn cuni n0 eldm2 opex elsnc
      exbii bitr biimp con1i unieqd uni0 syl6eq 1stval syl5eq eqtr4d pm2.61i )
      EGZFGZHZDIZFJZEJZDCGAGZIZABCKZLZDMLZIZWKWSEFWKWJWPLZWJMLZIWSXAWHWHWIWPNZW
      TWHWIEOZPWHQRWIQRXBWHIXCFOABCWHWIQQWNWHWPWHXCWNWHISWHWHIBGZWIIWHTUAWPTUBU
      CWHWIWPUDUEWKWTWQXAWRWJDWPUFWJDMUFUGUHUIWMUJZWQUKWRXEDWPULZRZUJWQUKIXGWMX
      GDQQUMZRDWJIZFJEJWMXFXHDWNQRZXDQRZUNZABUOWOCJZABUOXHXFXLXMABXLXMXJXKAOBOU
      PCAUQURUSABQQUTWOABCVAVBVCEFDVDXIWKEFDWJVEVFVGVHDWPVIVJXEDVKZULZVLZUKWRXE
      XPUKVLUKXEXOUKXOUKIZWMXQUJZWMXRWHXORZEJWMEXOVMXSWLEXSWJXNRZFJWLFWHXNXCVNX
      TWKFWJDWHWIVOVPVQVRVQVRVSVTWAWBWCDWDWEWFWG $.
      $( [5-Nov-2004] $) $( [14-Oct-2004] $)

    $( Value of an alternate definition of the ` 2nd ` function. $)
    2nd2val $p |- ( { <. <. x , y >. , z >. | z = y } ` A ) = ( 2nd ` A ) $=
      ( vw vv cv cop wceq wex copab2 cfv c2nd co visset op2nd cvv wcel equid
      a1i id eqid oprabval5 mp2an df-opr 3eqtr2r fveq2 eqeq12d mpbii 19.23aivv
      wn c0 cdm cxp wa copab pm3.2i a9e 2th opabbii df-xp dmoprab 3eqtr4r
      eleq2i elvv eqcom 2exbii 3bitr negbii ndmfv sylbir csn crn cuni n0 elrn2
      opex elsnc exbii bitr excom biimp con1i unieqd uni0 syl6eq 2ndval syl5eq
      eqtr4d pm2.61i ) EGZFGZHZDIZFJEJZDCGBGZIZABCKZLZDMLZIZWNXAEFWNWMWRLZWMMLZ
      IXAXCWLWKWLWRNZXBWKWLEOZFOZPWKQRWLQRXDWLIXEXFABCWKWLQQWPWLWRWPXFWPWPIAGZW
      KIBSTWPWLIUAWRUBUCUDWKWLWRUEUFWNXBWSXCWTWMDWRUGWMDMUGUHUIUJWOUKZWSULWTXHD
      WRUMZRZUKWSULIXJWOXJDQQUNZRDWMIZFJEJWOXIXKDXGQRZWPQRZUOZABUPWQCJZABUPXKXI
      XOXPABXOXPXMXNAOBOUQCBURUSUTABQQVAWQABCVBVCVDEFDVEXLWNEFDWMVFVGVHVIDWRVJV
      KXHDVLZVMZVNZULWTXHXSULVNULXHXRULXRULIZWOXTUKZWOYAWLXRRZFJWNEJZFJWOFXRVOY
      BYCFYBWMXQRZEJYCEWLXQXFVPYDWNEWMDWKWLVQVRVSVTVSWNFEWAVHWBWCWDWEWFDWGWHWIW
      J $.
      $( [22-Aug-2006] $) $( [10-Aug-2006] $)

    $( An alternate possible definition of the ` 1st ` function. $)
    df1st2 $p |- { <. <. x , y >. , z >. | z = x } = ( 1st |` ( V X. V ) ) $=
      ( vu vw vv cv wceq copab2 c1st cvv cxp cres cfv wral wfn wa wb visset
      wcel pm3.2i biantrur oprabbii fnoprab2 wss wf wfo fo1st fof ax-mp ffn
      ssv fnssres mp2an eqfnfv eqid cop 1st2val opelxp mpbir2an fvres eqtr4
      a1i rgen2 fveq2 eqeq12d ralxp mpbir ) CGAGZHZABCIZJKKLZMZHZVLVLHZDGZVKNZV
      PVMNZHZDVLOZVKVLPVMVLPZVNVOVTQRABCKKVIVKASZVJVIKTZBGKTZQZVJQABCWEVJWCWDWB
      BSUAUBUCUDJKPZVLKUEWAKKJUFZWFKKJUGWGUHKKJUIUJKKJUKUJVLULKVLJUMUNDVLVLVKVM
      UOUNVLUPVTEGZFGZUQZVKNZWJVMNZHZFKOEKOWMEFKWMWHKTZWIKTZQWKWJJNZWLABCWJURWJ
      VLTZWLWPHWQWNWOWHWIKKFSZUSESWRUTWJVLJVAUJVBVCVDVSWMDEFKKVPWJHVQWKVRWLVPWJ
      VKVEVPWJVMVEVFVGVHUT $.
      $( [14-Nov-2004] $) $( [14-Oct-2004] $)

    $( An alternate possible definition of the ` 2nd ` function. $)
    df2nd2 $p |- { <. <. x , y >. , z >. | z = y } = ( 2nd |` ( V X. V ) ) $=
      ( vu vw vv cv wceq copab2 c2nd cvv cxp cres cfv wral wfn wa wb visset
      wcel pm3.2i biantrur oprabbii fnoprab2 wss wf wfo fo2nd fof ax-mp ffn
      ssv fnssres mp2an eqfnfv eqid cop 2nd2val opelxp mpbir2an fvres eqtr4
      a1i rgen2 fveq2 eqeq12d ralxp mpbir ) CGBGZHZABCIZJKKLZMZHZVLVLHZDGZVKNZV
      PVMNZHZDVLOZVKVLPVMVLPZVNVOVTQRABCKKVIVKBSZVJAGKTZVIKTZQZVJQABCWEVJWCWDAS
      WBUAUBUCUDJKPZVLKUEWAKKJUFZWFKKJUGWGUHKKJUIUJKKJUKUJVLULKVLJUMUNDVLVLVKVM
      UOUNVLUPVTEGZFGZUQZVKNZWJVMNZHZFKOEKOWMEFKWMWHKTZWIKTZQWKWJJNZWLABCWJURWJ
      VLTZWLWPHWQWNWOWHWIKKFSZUSESWRUTWJVLJVAUJVBVCVDVSWMDEFKKVPWJHVQWKVRWLVPWJ
      VKVEVPWJVMVEVFVGVHUT $.
      $( [?] $) $( [10-Aug-2006] $)
  $}

  $( Membership in a cross product.  This version requires no quantifiers or
     dummy variables.  See also ~ elxp4 . $)
  elxp6 $p |- ( A e. ( B X. C ) <-> ( A = <. ( 1st ` A ) , ( 2nd ` A ) >.
               /\ ( ( 1st ` A ) e. B /\ ( 2nd ` A ) e. C ) ) ) $=
    ( cxp wcel csn cdm cuni crn cop wceq wa c1st cfv c2nd elxp4 1stval 2ndval
    opeq12i eqeq2i eleq1i anbi12i bitr4 ) ABCDEAAFZGHZUDIHZJZKZUEBEZUFCEZLZLAAM
    NZAONZJZKZULBEZUMCEZLZLABCPUOUHURUKUNUGAULUEUMUFAQZARZSTUPUIUQUJULUEBUSUAUM
    UFCUTUAUBUBUC $.
    $( [14-Oct-2004] $) $( [9-Oct-2004] $)

  $( Membership in a cross product.  This version requires no quantifiers or
     dummy variables.  See also ~ elxp4 . $)
  elxp7 $p |- ( A e. ( B X. C ) <-> ( A e. ( V X. V )
               /\ ( ( 1st ` A ) e. B /\ ( 2nd ` A ) e. C ) ) ) $=
    ( cxp wcel c1st cfv c2nd cop wceq wa cvv elxp6 fvex pm3.2i mpbiran2 anbi1i
    bitr4 ) ABCDEAAFGZAHGZIJZSBETCEKZKALLDEZUBKABCMUCUAUBUCUASLEZTLEZKALLMUDUEA
    FNAHNOPQR $.
    $( [19-Aug-2006] $) $( [19-Aug-2006] $)

  ${
    $d x A $.  $d x B $.
    $( Representation of cross product based on ordered pair component
       functions. $)
    xp2 $p |- ( A X. B ) = { x e. ( V X. V ) | ( ( 1st ` x ) e. A /\
              ( 2nd ` x ) e. B ) } $=
      ( cxp cv cvv wcel c1st cfv c2nd wa cab crab elxp7 abbi2i df-rab eqtr4 )
      BCDZAEZFFDZGSHIBGSJICGKZKZALUAATMUBARSBCNOUAATPQ $.
      $( [23-Oct-2006] $) $( [16-Sep-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( The membership relation for a cross product is inherited by union. $)
    unielxp $p |- ( A e. ( B X. C ) -> U. A e. U. ( B X. C ) ) $=
      ( vx cxp wcel cvv c1st cfv c2nd wa cuni elxp7 elvvuni adantr cv cab wex
      simprl wceq eleq2 eleq1 fveq2 eleq1d anbi12d cla4egv mpcom eluniab
      sylibr crab xp2 df-rab eqtr unieqi syl6eleqr mpancom sylbi ) ABCEZFAGGEZF
      ZAHIZBFZAJIZCFZKZKZALZURLZFZABCMVGAFZVFVIUTVJVEANOVJVFKZVGDPZUSFZVLHIZBFZ
      VLJIZCFZKZKZDQZLZVHVKVGVLFZVSKZDRZVGWAFUTVKWDVJUTVESWCVKDAUSVLATZWBVJVSVF
      VLAVGUAWEVMUTVRVEVLAUSUBWEVOVBVQVDWEVNVABVLAHUCUDWEVPVCCVLAJUCUDUEUEUEUFU
      GVSDVGUHUIURVTURVRDUSUJVTDBCUKVRDUSULUMUNUOUPUQ $.
      $( [14-Nov-2006] $) $( [16-Sep-2006] $)
  $}

  $( Reconstruction of a member of a relation in terms of its ordered pair
     components. $)
  1st2nd $p |- ( ( Rel B /\ A e. B ) ->
               A = <. ( 1st ` A ) , ( 2nd ` A ) >. ) $=
    ( wrel wcel wa cvv cxp c1st cfv c2nd cop wceq wss ssel2 df-rel sylanb
    elxp6 pm3.26bd syl ) BCZABDZEAFFGZDZAAHIZAJIZKLZBUBMUAUCTBUBANBOPUCUFUDFDUE
    FDEAFFQRS $.
    $( [30-Aug-2006] $) $( [29-Aug-2006] $)

  $( The first ordered pair component of a member of a relation belongs to
     the domain of the relation. $)
  1stdm $p |- ( ( Rel R /\ A e. R ) -> ( 1st ` A ) e. dom R ) $=
    ( wrel wcel wa c1st cfv cint cdm cvv cxp wceq wss df-rel biimp sseld imp
    1stval2 syl elreldm eqeltrd ) BCZABDZEZAFGZAHHZBIUDAJJKZDZUEUFLUBUCUHUBBUGA
    UBBUGMBNOPQARSBATUA $.
    $( [?] $) $( [17-Sep-2006] $)

  $( The second ordered pair component of a member of a relation belongs to
     the range of the relation. $)
  2ndrn $p |- ( ( Rel R /\ A e. R ) -> ( 2nd ` A ) e. ran R ) $=
    ( wrel wcel wa c1st cfv c2nd cop crn 1st2nd pm3.27 eqeltrrd fvex opelrn
    syl ) BCZABDZEZAFGZAHGZIZBDUABJDSAUBBABKQRLMTUABAFNAHNOP $.
    $( [?] $) $( [17-Sep-2006] $)

  $( Equality theorem for substitution of a class for an ordered pair (analog
     of ~ sbceq1a , that avoids the existential quantifiers of ~ copsexg ). $)
  sbcopeq1a $p |- ( <. x , y >. = A -> ( ph <->
                  [ ( 1st ` A ) / x ] [ ( 2nd ` A ) / y ] ph ) ) $=
    ( cv c2nd cfv wceq c1st wsbc wb cop sbceq1a sylan9bb fveq2 visset op2nd
    syl5eqr op1st sylanc ) CEZDFGZHZBEZDIGZHZAACUBJZBUEJZKUDUALZDHZUCAUGUFUHACU
    BMUGBUEMNUJUIFGUBUAUIDFOUDUABPZCPQRUJUIIGUEUDUIDIOUDUAUKSRT $.
    $( [?] $) $( [19-Aug-2006] $)

  $( Equality theorem for substitution of a class ` A ` for an ordered pair
     ` <. x , y >. ` in ` B ` (analog of ~ csbeq1a ). $)
  csbopeq1a $p |- ( <. x , y >. = A -> B =
                  [_ ( 1st ` A ) / x ]_ [_ ( 2nd ` A ) / y ]_ B ) $=
    ( cv c2nd cfv wceq c1st csb cop csbeq1a sylan9eq fveq2 visset op2nd
    syl5eqr op1st sylanc ) BEZCFGZHZAEZCIGZHZDAUDBUADJZJZHUCTKZCHZUBUEDUFUGBUAD
    LAUDUFLMUIUHFGUATUHCFNUCTAOZBOPQUIUHIGUDUCUHCINUCTUJRQS $.
    $( [22-Aug-2006] $) $( [19-Aug-2006] $)

  ${
    $d z ph $.  $d x y z $.
    $( A way to define an ordered-pair class abstraction without using
       existential quantifiers. $)
    dfopab2 $p |- { <. x , y >. | ph } = { z | ( z e. ( V X. V ) /\
                  [ ( 1st ` z ) / x ] [ ( 2nd ` z ) / y ] ph ) } $=
      ( copab cv cop wceq wa wex cab cvv cxp wcel c2nd cfv wsbc c1st df-opab
      fvex hbsbc1v 19.41 wb fveq2 visset op2nd syl6req sbceq1a syl op1st bitrd
      pm5.32i exbii wal wi ax-17 hbsbcg ax-mp bitr elvv anbi1i 3bitr4 abbii
      eqtr ) ABCEDFZBFZCFZGZHZAIZCJZBJZDKVELLMNZACVEOPZQZBVERPZQZIZDKABCDSVLVRD
      VICJZVQIZBJVSBJZVQIVLVRVSVQBVOBVPVERTZUAUBVKVTBVKVIVQIZCJVTVJWCCVIAVQVIAV
      OVQVIVGVNHAVOUCVIVNVHOPVGVEVHOUDVFVGBUEZCUEUFUGACVNUHUIVIVFVPHVOVQUCVIVPV
      HRPVFVEVHRUDVFVGWDUJUGVOBVPUHUIUKULUMVIVQCVPLNVQVQCUNUOWBVOCBBVPLVFVPNCUP
      ACVNVEOTUAUQURUBUSUMVMWAVQBCVEUTVAVBVCVD $.
      $( [19-Oct-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d w ph $.  $d x y z w $.
    $( A way to define an operation class abstraction without using existential
       quantifiers. $)
    dfoprab3 $p |- { <. <. x , y >. , z >. | ph } = { <. w , z >. |
         ( w e. ( V X. V ) /\ [ ( 1st ` w ) / x ] [ ( 2nd ` w ) / y ] ph ) } $=
      ( copab2 cv cop wceq wa wex copab cvv cxp wcel c2nd cfv wsbc c1st
      dfoprab2 fvex hbsbc1v 19.41 wb fveq2 visset op2nd syl6req sbceq1a syl
      op1st bitrd pm5.32i exbii wal wi ax-17 hbsbcg ax-mp bitr elvv anbi1i
      3bitr4 opabbii eqtr ) ABCDFEGZBGZCGZHZIZAJZCKZBKZEDLVFMMNOZACVFPQZRZBVFSQ
      ZRZJZEDLABCDETVMVSEDVJCKZVRJZBKVTBKZVRJVMVSVTVRBVPBVQVFSUAZUBUCVLWABVLVJV
      RJZCKWAVKWDCVJAVRVJAVPVRVJVHVOIAVPUDVJVOVIPQVHVFVIPUEVGVHBUFZCUFUGUHACVOU
      IUJVJVGVQIVPVRUDVJVQVISQVGVFVISUEVGVHWEUKUHVPBVQUIUJULUMUNVJVRCVQMOVRVRCU
      OUPWCVPCBBVQMVGVQOCUQACVOVFPUAUBURUSUCUTUNVNWBVRBCVFVAVBVCVDVE $.
      $( [7-Sep-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d x y A $.  $d x y ch $.
    elopabi.1 $e |- ( x = ( 1st ` A ) -> ( ph <-> ps ) ) $.
    elopabi.2 $e |- ( y = ( 2nd ` A ) -> ( ps <-> ch ) ) $.
    $( A consequence of membership in an ordered-pair class abstraction, using
       ordered pair extractors. $)
    elopabi $p |- ( A e. { <. x , y >. | ph } -> ch ) $=
      ( copab wcel c1st cfv c2nd cop wceq wrel relopab 1st2nd mpan eleq1 fvex
      opelopab syl6bb biimpcd mpd ) FADEIZJZFFKLZFMLZNZOZCUFPUGUKADEQFUFRSUKUGC
      UKUGUJUFJCFUJUFTABCDEUHUIFKUAFMUAGHUBUCUDUE $.
      $( [3-Sep-2006] $) $( [29-Aug-2006] $)
  $}

  ${
    $d w x y z A $.  $d x y ch $.  $d w ph $.  $d x ps $.  $d x y z th $.
    eloprabi.1 $e |- ( x = ( 1st ` ( 1st ` A ) ) -> ( ph <-> ps ) ) $.
    eloprabi.2 $e |- ( y = ( 2nd ` ( 1st ` A ) ) -> ( ps <-> ch ) ) $.
    eloprabi.3 $e |- ( z = ( 2nd ` A ) -> ( ch <-> th ) ) $.
    $( A consequence of membership in an operation class abstraction, using
       ordered pair extractors. $)
    eloprabi $p |- ( A e. { <. <. x , y >. , z >. | ph } -> th ) $=
      ( vw copab2 wcel c1st cfv c2nd cop wceq wrel reloprab 1st2nd mpan cv cvv
      cxp wsbc wa copab dfoprab3 eleq2i pm3.26 ssopab2i sseli eleq1 pm4.2i
      elopabi relxp 3syl sylbi opeq1d eqtrd wb fvex eloprabg mp3an syl6bb
      biimpcd mpd ) HAEFGMZNZHHOPZOPZVLQPZRZHQPZRZSZDVKHVLVPRZVQVJTVKHVSSAEFGUA
      HVJUBUCVKVLVOVPVKHLUDZUEUEUFZNZAFVTQPUGEVTOPUGZUHZLGUIZNZVLVOSZVJWEHAEFGL
      UJUKWFHWBLGUIZNVLWANZWGWEWHHWDWBLGWBWCULUMUNWBWIWILGHVTVLWAUOGUDVPSWIUPUQ
      WATWIWGUEUEURVLWAUBUCUSUTVAVBVRVKDVRVKVQVJNZDHVQVJUOVMUENVNUENVPUENWJDVCV
      LOVDVLQVDHQVDABCDEFGVMVNVPUEUEUEIJKVEVFVGVHVI $.
      $( [16-Nov-2006] $) $( [6-Nov-2006] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w z C $.  $d w x y z D $.
    foprab2.1 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Mapping of an operation class abstraction. $)
    foprab2 $p |- ( A. x e. A A. y e. B C e. D <-> F : ( A X. B ) --> D ) $=
      ( vw wcel wral cv c1st cfv c2nd csb cxp wf fvex ax-17 hbcsb1 hbel cvv
      wal wi hbcsbg ax-mp cop wceq csbopeq1a eqcoms eqcomd eleq1d ralxpf
      bicomi wa copab2 copab wsbc dfoprab3 elxp7 anbi1i anass bitr wb sbcang
      sbcgf sbcel1gv anbi12i sbceq2dig sbcbii anbi2i opabbii eqcomi eqtr
      fopab2 ) FGKZBELADLZAJMZNOZBVTPOZFQZQZGKZJDERZLZWFGHSWGVSWEVRJABDEACCWDGA
      CWAWCVTNTZCMZWAKZAUAUBWIGKZAUAUCBCCWDGWAUDKZWIWDKZWMBUEUFWHBACWAWCUDWJBUA
      BCWBFVTPTZWIWBKBUAUBUGUHWKBUAUCVRJUAVTAMZBMZUIZUJZWDFGWRFWDFWDUJWQVTABVTF
      UKULUMUNUOUPJCWFGWDHHWODKZWPEKZUQZWIFUJZUQZABCURZVTWFKZWIWDUJZUQZJCUSZIXD
      VTUDUDRKZXCBWBUTZAWAUTZUQZJCUSZXHXCABCJVAXHXMXGXLJCXGXIWADKZWBEKZUQZXFUQZ
      UQZXLXGXIXPUQZXFUQXRXEXSXFVTDEVBVCXIXPXFVDVEXLXRXKXQXIXKWSXOUQZWIWCUJZUQZ
      AWAUTZXQWLXKYCVFWHXJYBAWAUDXJXABWBUTZXBBWBUTZUQZYBWBUDKZXJYFVFWNXAXBBWBUD
      VGUHYDXTYEYAYDWSBWBUTZWTBWBUTZUQZXTYGYDYJVFWNWSWTBWBUDVGUHYHWSYIXOYGYHWSV
      FWNWSBWBUDWSBUAVHUHYGYIXOVFWNBWBEUDVIUHVJVEYGYEYAVFWNBWBWIFUDVKUHVJVEVLUH
      YCXTAWAUTZYAAWAUTZUQZXQWLYCYMVFWHXTYAAWAUDVGUHYKXPYLXFYKWSAWAUTZXOAWAUTZU
      QZXPWLYKYPVFWHWSXOAWAUDVGUHYNXNYOXOWLYNXNVFWHAWADUDVIUHWLYOXOVFWHXOAWAUDX
      OAUAVHUHVJVEWLYLXFVFWHAWAWIWCUDVKUHVJVEVEVMUPVEVNVOVPVPVQVE $.
      $( [17-Sep-2006] $) $( [18-Aug-2006] $)
  $}


