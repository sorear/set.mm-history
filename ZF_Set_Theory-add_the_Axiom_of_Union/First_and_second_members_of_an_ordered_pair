$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        First and second members of an ordered pair
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c 1st $. $( First member of an ordered pair $)
  $c 2nd $. $( Second member of an ordered pair $)

  $( Extend the definition of a class to include the first member an
     ordered pair function. $)
  c1st $a class 1st $.

  $( Extend the definition of a class to include the second member an
     ordered pair function. $)
  c2nd $a class 2nd $.

  ${
    $d x y z $.
    $( Define a function that extracts the first member of an ordered pair.
       Theorem ~ op1st proves that it does this.  Equivalent to Definition 5.13
       (i) of [Monk1] p. 52 (compare ~ op1sta and ~ op1stb ).  The notation is
       the same as Monk's. $)
    df-1st $a |- 1st = { <. x , y >. | y = U. dom { x } } $.

    $( Define a function that extracts the second member of an ordered pair.
       Theorem ~ op2nd proves that it does this.  Equivalent to Definition 5.13
       (ii) of [Monk1] p. 52 (compare ~ op2nda and ~ op2ndb ).  The notation is
       the same as Monk's. $)
    df-2nd $a |- 2nd = { <. x , y >. | y = U. ran { x } } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The value of the function that extracts the first member of an ordered
       pair. $)
    1stval $p |- ( 1st ` A ) = U. dom { A } $=
      ( vy vx cvv wcel c1st cfv csn cdm cuni wceq snex dmexg ax-mp uniex wa cv
      copab sneq dmeqd unieqd fvopabg df-1st fveq1i syl5eq mpan2 wn c0 fvprc
      snprc biimp dm0 syl6eq uni0 eqtr4d pm2.61i ) ADEZAFGZAHZIZJZKZUQVADEZVBUT
      USDEUTDEALUSDMNOUQVCPABQCQZHZIZJZKCBRZGVAURCBAVGVADDVDAKZVFUTVIVEUSVDASTU
      AUBAFVHCBUCUDUEUFUQUGZURUHVAAFUIVJVAUHJUHVJUTUHVJUTUHIUHVJUSUHVJUSUHKAUJU
      KTULUMUAUNUMUOUP $.
      $( [14-Oct-2004] $) $( [9-Oct-2004] $)

    $( The value of the function that extracts the second member of an ordered
       pair. $)
    2ndval $p |- ( 2nd ` A ) = U. ran { A } $=
      ( vy vx cvv wcel c2nd cfv csn crn cuni wceq snex rnexg ax-mp uniex wa cv
      copab sneq rneqd unieqd fvopabg df-2nd fveq1i syl5eq mpan2 wn c0 fvprc
      snprc biimp rn0 syl6eq uni0 eqtr4d pm2.61i ) ADEZAFGZAHZIZJZKZUQVADEZVBUT
      USDEUTDEALUSDMNOUQVCPABQCQZHZIZJZKCBRZGVAURCBAVGVADDVDAKZVFUTVIVEUSVDASTU
      AUBAFVHCBUCUDUEUFUQUGZURUHVAAFUIVJVAUHJUHVJUTUHVJUTUHIUHVJUSUHVJUSUHKAUJU
      KTULUMUAUNUMUOUP $.
      $( [14-Oct-2004] $) $( [9-Oct-2004] $)
  $}

  $( The value of the first-member function at the empty set. $)
  1st0 $p |- ( 1st ` (/) ) = (/) $=
    ( c0 c1st cfv csn cdm cuni 1stval dmsn0 unieqi uni0 3eqtr ) ABCADEZFAFAAGLA
    HIJK $.
    $( [26-Apr-2007] $) $( [23-Apr-2007] $)

  $( The value of the second-member function at the empty set. $)
  2nd0 $p |- ( 2nd ` (/) ) = (/) $=
    ( c0 c2nd cfv csn crn cuni 2ndval cdm wceq dmsn0 dm0rn0 mpbi unieqi uni0
    3eqtr ) ABCADZEZFAFAAGQAPHAIQAIJPKLMNO $.
    $( [1-May-2007] $) $( [23-Apr-2007] $)

  ${
    op1st.1 $e |- A e. V $.
    $( Extract the first member of an ordered pair. $)
    op1st $p |- ( 1st ` <. A , B >. ) = A $=
      ( cop c1st cfv csn cdm cuni 1stval op1sta eqtr ) ABDZEFMGHIAMJABCKL $.
      $( [16-Oct-2004] $) $( [5-Oct-2004] $)

    ${
      op2n.2 $e |- B e. V $.
      $( Extract the second member of an ordered pair. $)
      op2nd $p |- ( 2nd ` <. A , B >. ) = B $=
        ( cop c2nd cfv csn crn cuni 2ndval op2nda eqtr ) ABEZFGNHIJBNKABCDLM $.
        $( [16-Oct-2004] $) $( [5-Oct-2004] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Extract the first member of an ordered pair. $)
    op1stg $p |- ( A e. C -> ( 1st ` <. A , B >. ) = A ) $=
      ( vx cv cop c1st cfv wceq opeq1 fveq2d id eqeq12d visset op1st vtoclg )
      DEZBFZGHZQIABFZGHZAIDACQAIZSUAQAUBRTGQABJKUBLMQBDNOP $.
      $( [20-Jul-2005] $) $( [19-Jul-2005] $)

    $( Extract the second member of an ordered pair. $)
    op2ndg $p |- ( ( A e. C /\ B e. D ) -> ( 2nd ` <. A , B >. ) = B ) $=
      ( vx vy cv cop c2nd cfv wceq opeq1 fveq2d eqeq1d opeq2 id eqeq12d visset
      op2nd vtocl2g ) EGZFGZHZIJZUBKAUBHZIJZUBKABHZIJZBKEFABCDUAAKZUDUFUBUIUCUE
      IUAAUBLMNUBBKZUFUHUBBUJUEUGIUBBAOMUJPQUAUBERFRST $.
      $( [24-Jul-2005] $) $( [19-Jul-2005] $)

    $( Alternate value of the function that extracts the first member of an
       ordered pair.  Definition 5.13 (i) of [Monk1] p. 52. $)
    1stval2 $p |- ( A e. ( V X. V ) -> ( 1st ` A ) = |^| |^| A ) $=
      ( vx vy cvv cxp wcel cv cop wceq wex c1st cfv cint elvv visset op1st
      op1stb eqtr4 fveq2 inteq inteqd 3eqtr4a 19.23aivv sylbi ) ADDEFABGZCGZHZI
      ZCJBJAKLZAMZMZIZBCANUHULBCUHUGKLZUGMZMZUIUKUMUEUOUEUFBOZPUEUFUPQRAUGKSUHU
      JUNAUGTUAUBUCUD $.
      $( [14-Oct-2006] $) $( [18-Aug-2006] $)

    $( Alternate value of the function that extracts the second member of an
       ordered pair.  Definition 5.13 (ii) of [Monk1] p. 52. $)
    2ndval2 $p |- ( A e. ( V X. V ) -> ( 2nd ` A ) = |^| |^| |^| `' { A } ) $=
      ( vx vy cvv cxp wcel cv cop wceq wex c2nd cfv csn ccnv cint elvv visset
      op2nd op2ndb eqtr4 fveq2 sneq cnveq syl inteqd 3eqtr4a 19.23aivv sylbi )
      ADDEFABGZCGZHZIZCJBJAKLZAMZNZOZOZOZIZBCAPULUSBCULUKKLZUKMZNZOZOZOZUMURUTU
      JVEUIUJBQZCQZRUIUJVFVGSTAUKKUAULUQVDULUPVCULUOVBULUNVAIUOVBIAUKUBUNVAUCUD
      UEUEUEUFUGUH $.
      $( [18-Aug-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d x y z w v A $.  $d x y z B $.  $d x y z w v u $.
    $( The ` 1st ` function maps the universe onto the universe. $)
    fo1st $p |- 1st : V -onto-> V $=
      ( vy vx cvv c1st wfo cv csn cdm cuni wceq copab wfn crn df-fo wcel snex
      dmexg ax-mp uniex wa visset biantrur opabbii fnopab2 wex cab cop op1sta
      eqcomi opex sneq dmeqd unieqd eqeq2d cla4ev equid 2th abbii rnopab df-v
      3eqtr4 mpbir2an wb df-1st foeq1 mpbir ) CCDEZCCAFZBFZGZHZIZJZBAKZEZVOVNCL
      VNMZCJCCVNNBACVLVNVKVJCOVKCOVIPVJCQRSVMVICOZVMTBAVQVMBUAUBUCUDVMBUEZAUFVH
      VHJZAUFVPCVRVSAVRVSVHVHVHUGZGZHZIZJZVRWCVHVHVHAUAUHUIVMWDBVTVHVHUJVIVTJZV
      LWCVHWEVKWBWEVJWAVIVTUKULUMUNUORAUPUQURVMBAUSAUTVAVBDVNJVGVOVCBAVDCCDVNVE
      RVF $.
      $( [16-Oct-2004] $) $( [14-Oct-2004] $)

    $d x y z w v A $.  $d x y z B $.  $d x y z w v u $.
    $( The ` 2nd ` function maps the universe onto the universe. $)
    fo2nd $p |- 2nd : V -onto-> V $=
      ( vy vx cvv c2nd wfo cv csn crn cuni wceq copab wfn df-fo wcel snex
      rnexg ax-mp uniex wa visset biantrur opabbii fnopab2 wex cab cop op2nda
      eqcomi opex sneq rneqd unieqd eqeq2d cla4ev equid 2th abbii rnopab df-v
      3eqtr4 mpbir2an wb df-2nd foeq1 mpbir ) CCDEZCCAFZBFZGZHZIZJZBAKZEZVNVMCL
      VMHZCJCCVMMBACVKVMVJVICNVJCNVHOVICPQRVLVHCNZVLSBAVPVLBTUAUBUCVLBUDZAUEVGV
      GJZAUEVOCVQVRAVQVRVGVGVGUFZGZHZIZJZVQWBVGVGVGATZWDUGUHVLWCBVSVGVGUIVHVSJZ
      VKWBVGWEVJWAWEVIVTVHVSUJUKULUMUNQAUOUPUQVLBAURAUSUTVADVMJVFVNVBBAVCCCDVMV
      DQVE $.
      $( [16-Oct-2004] $) $( [14-Oct-2004] $)

    $( Mapping of a restriction of the ` 1st ` (first member of an ordered
       pair) function. $)
    f1stres $p |- ( 1st |` ( A X. B ) ) : ( A X. B ) --> A $=
      ( vx vy vz cv csn cdm cuni wcel cxp wral c1st cres wf cop visset op1sta
      eleq1i biimpr adantr rgen2 wceq sneq dmeqd unieqd eleq1d ralxp mpbir
      copab wa df-1st reseq1 ax-mp resopab eqtr fopab2 mpbi ) CFZGZHZIZAJZCABKZ
      LZVDAMVDNZOVEDFZEFZPZGZHZIZAJZEBLDALVMDEABVGAJZVMVHBJVMVNVLVGAVGVHDQRSTUA
      UBVCVMCDEABUSVIUCZVBVLAVOVAVKVOUTVJUSVIUDUEUFUGUHUICDVDAVBVFVFVGVBUCZCDUJ
      ZVDNZUSVDJVPUKCDUJMVQUCVFVRUCCDULMVQVDUMUNVPCDVDUOUPUQUR $.
      $( [16-Oct-2004] $) $( [11-Oct-2004] $)

    $( Mapping of a restriction of the ` 2nd ` (second member of an ordered
       pair) function. $)
    f2ndres $p |- ( 2nd |` ( A X. B ) ) : ( A X. B ) --> B $=
      ( vx vy vz cv csn crn cuni wcel cxp wral c2nd cres wf cop visset op2nda
      eleq1i biimpr adantl rgen2 wceq sneq rneqd unieqd eleq1d ralxp mpbir
      copab wa df-2nd reseq1 ax-mp resopab eqtr fopab2 mpbi ) CFZGZHZIZBJZCABKZ
      LZVDBMVDNZOVEDFZEFZPZGZHZIZBJZEBLDALVMDEABVHBJZVMVGAJVMVNVLVHBVGVHDQEQRST
      UAUBVCVMCDEABUSVIUCZVBVLBVOVAVKVOUTVJUSVIUDUEUFUGUHUICDVDBVBVFVFVGVBUCZCD
      UJZVDNZUSVDJVPUKCDUJMVQUCVFVRUCCDULMVQVDUMUNVPCDVDUOUPUQUR $.
      $( [10-Mar-2007] $) $( [7-Aug-2006] $)

    $( Value of an alternate definition of the ` 1st ` function. $)
    1st2val $p |- ( { <. <. x , y >. , z >. | z = x } ` A ) = ( 1st ` A ) $=
      ( vw vv cv cop wceq wex copab2 cfv c1st co visset op1st cvv wcel id eqid
      a1i oprabval5 mp2an df-opr 3eqtr2r fveq2 eqeq12d mpbii 19.23aivv wn c0
      cdm cxp wa copab pm3.2i a9e 2th opabbii df-xp dmoprab 3eqtr4r eleq2i
      elvv eqcom 2exbii 3bitr negbii ndmfv sylbir csn cuni n0 eldm2 opex elsnc
      exbii bitr biimp con1i unieqd uni0 syl6eq 1stval syl5eq eqtr4d pm2.61i )
      EGZFGZHZDIZFJZEJZDCGAGZIZABCKZLZDMLZIZWKWSEFWKWJWPLZWJMLZIWSXAWHWHWIWPNZW
      TWHWIEOZPWHQRWIQRXBWHIXCFOABCWHWIQQWNWHWPWHXCWNWHISWHWHIBGZWIIWHTUAWPTUBU
      CWHWIWPUDUEWKWTWQXAWRWJDWPUFWJDMUFUGUHUIWMUJZWQUKWRXEDWPULZRZUJWQUKIXGWMX
      GDQQUMZRDWJIZFJEJWMXFXHDWNQRZXDQRZUNZABUOWOCJZABUOXHXFXLXMABXLXMXJXKAOBOU
      PCAUQURUSABQQUTWOABCVAVBVCEFDVDXIWKEFDWJVEVFVGVHDWPVIVJXEDVKZULZVLZUKWRXE
      XPUKVLUKXEXOUKXOUKIZWMXQUJZWMXRWHXORZEJWMEXOVMXSWLEXSWJXNRZFJWLFWHXNXCVNX
      TWKFWJDWHWIVOVPVQVRVQVRVSVTWAWBWCDWDWEWFWG $.
      $( [5-Nov-2004] $) $( [14-Oct-2004] $)

    $( Value of an alternate definition of the ` 2nd ` function. $)
    2nd2val $p |- ( { <. <. x , y >. , z >. | z = y } ` A ) = ( 2nd ` A ) $=
      ( vw vv cv cop wceq wex copab2 cfv c2nd co visset op2nd cvv wcel equid
      a1i id eqid oprabval5 mp2an df-opr 3eqtr2r fveq2 eqeq12d mpbii 19.23aivv
      wn c0 cdm cxp wa copab pm3.2i a9e 2th opabbii df-xp dmoprab 3eqtr4r
      eleq2i elvv eqcom 2exbii 3bitr negbii ndmfv sylbir csn crn cuni n0 elrn2
      opex elsnc exbii bitr excom biimp con1i unieqd uni0 syl6eq 2ndval syl5eq
      eqtr4d pm2.61i ) EGZFGZHZDIZFJEJZDCGBGZIZABCKZLZDMLZIZWNXAEFWNWMWRLZWMMLZ
      IXAXCWLWKWLWRNZXBWKWLEOZFOZPWKQRWLQRXDWLIXEXFABCWKWLQQWPWLWRWPXFWPWPIAGZW
      KIBSTWPWLIUAWRUBUCUDWKWLWRUEUFWNXBWSXCWTWMDWRUGWMDMUGUHUIUJWOUKZWSULWTXHD
      WRUMZRZUKWSULIXJWOXJDQQUNZRDWMIZFJEJWOXIXKDXGQRZWPQRZUOZABUPWQCJZABUPXKXI
      XOXPABXOXPXMXNAOBOUQCBURUSUTABQQVAWQABCVBVCVDEFDVEXLWNEFDWMVFVGVHVIDWRVJV
      KXHDVLZVMZVNZULWTXHXSULVNULXHXRULXRULIZWOXTUKZWOYAWLXRRZFJWNEJZFJWOFXRVOY
      BYCFYBWMXQRZEJYCEWLXQXFVPYDWNEWMDWKWLVQVRVSVTVSWNFEWAVHWBWCWDWEWFDWGWHWIW
      J $.
      $( [22-Aug-2006] $) $( [10-Aug-2006] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.
    2ndconst.1 $e |- A e. V $.
    $( The mapping of a restriction of the ` 2nd ` function to a converse
       constant function. $)
    2ndconstOLD $p |- ( 2nd |` ( { A } X. B ) ) : ( { A } X. B ) -1-1-onto-> B $=
      ( vy vx vw vz csn cxp c2nd cres wf1o wfo ccnv wfun f1o3 wf crn wceq
      dffo2 f2ndres cima df-ima cv wcel wbr wa wex visset elima2 cop ancom cfv
      weq an12 fveq2 op2nd syl6eq eqeq1d anbi1d pm5.32i 3bitr exbii opeq2
      eqeq2d eleq1 anbi12d ceqsexv 3bitrr 19.42v cvv wfn wb fo2nd fof ax-mp
      ffn fnbrfvb mp2an elxp excom elsn anbi1i anbi2i bitr opeq1 anbi12i opex
      isseti mpbiran2 eqriv eqtr3 mpbir2an wmo funcnv2 moeq moani brres mobii
      mpbir mpgbir ) AHZBIZBJXCKZLXCBXDMZXDNOZXCBXDPXEXCBXDQXDRZBSXCBXDTXBBUAJX
      CUBZXGBJXCUCDXHBDUDZXHUEEUDZXCUEZXJXIJUFZUGZEUHZXIBUEZEXIJXCDUIZUJXNXOXJA
      XIUKZSZEUHZXNXOXRUGZEUHXOXSUGXMXTEXMXLXKUGZXTXKXLULXTXJJUMZXISZXJAFUDZUKZ
      SZYDBUEZUGZUGZFUHZYCYHFUHZUGYAYJFDUNZYHUGZFUHXRXOUGZXTYIYMFYIYFYCYGUGZUGY
      FYLYGUGZUGYMYCYFYGUOYFYOYPYFYCYLYGYFYBYDXIYFYBYEJUMYDXJYEJUPAYDCFUIUQURUS
      UTVAYFYLYGUOVBVCYHYNFXIXPYLYFXRYGXOYLYEXQXJYDXIAVDVEYDXIBVFVGVHXRXOULVIYC
      YHFVJYCXLYKXKJVKVLZXJVKUEYCXLVMVKVKJQZYQVKVKJMYRVNVKVKJVOVPVKVKJVQVPEUIVK
      XJXIJXPVRVSXKXJGUDZYDUKZSZYSXBUEZYGUGZUGZFUHGUHUUDGUHZFUHYKGFXJXBBVTUUDGF
      WAUUEYHFUUEYSASZUUAYGUGZUGZGUHYHUUDUUHGUUDUUAUUFYGUGZUGUUHUUCUUIUUAUUBUUF
      YGGAWBWCWDUUAUUFYGUOWEVCUUGYHGACUUFUUAYFYGUUFYTYEXJYSAYDWFVEUTVHWEVCVIWGV
      IZWEVCXOXREVJWEEXQAXIWHWIWJWEWKWLWMXFXJXIXDUFZEWNZDEDXDWOUULXTEWNXRXOEEXQ
      WPWQUUKXTEUUKYAXTXJXIJXCXPWRUUJWEWSWTXAWM $.
      $( [27-Mar-2008] $) $( [27-Mar-2008] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y C $.
    $( The mapping of a restriction of the ` 2nd ` function to a converse
       constant function. $)
    2ndconst $p |- ( A e. C ->
             ( 2nd |` ( { A } X. B ) ) : ( { A } X. B ) -1-1-onto-> B ) $=
      ( vy vx vw vz wcel csn cxp c2nd cres wfo ccnv wfun wa wf1o wf crn wceq
      cima cv wbr wex cop cfv fveq2 cvv visset op2ndg mpan2 sylan9eqr eqeq1d
      anbi1d pm5.32da an12 3bitr4g exbidv opeq2 eqeq2d eleq1 anbi12d ceqsexv
      ancom bitr syl6rbb opeq1 ceqsexgv elsn anbi1i anbi2i exbii syl5bb excom
      elxp syl5rbb anbi2d 19.42v wfn wb fo2nd fof ax-mp ffn fnbrfvb mp2an
      bitr2d opex isseti mpbiran2 syl6bb elima2 eqrdv df-ima syl5eqr f2ndres
      jctil dffo2 sylibr wmo wal moeq moani ax-17 brres mobid mpbiri 19.21aiv
      funcnv2 jca f1o3 ) ACHZAIZBJZBKXNLZMZXONOZPXNBXOQXLXPXQXLXNBXORZXOSZBTZPX
      PXLXTXRXLKXNUAZBXSXLDYABXLEUBZXNHZYBDUBZKUCZPZEUDZYDBHZYDYAHXLYGYHYBAYDUE
      ZTZPZEUDZYHXLYFYKEXLYEYCPZYKYFXLYKYBKUFZYDTZYBAFUBZUEZTZYPBHZPZPZFUDZYMXL
      UUBYPYDTZYTPZFUDZYKXLUUAUUDFXLYRYOYSPZPYRUUCYSPZPUUAUUDXLYRUUFUUGXLYRPZYO
      UUCYSUUHYNYPYDYRXLYNYQKUFZYPYBYQKUGXLYPUHHUUIYPTFUIAYPCUHUJUKULUMUNUOYOYR
      YSUPUUCYRYSUPUQURUUEYJYHPZYKYTUUJFYDDUIZUUCYRYJYSYHUUCYQYIYBYPYDAUSUTYPYD
      BVAVBVCYJYHVDVEVFXLYEYTFUDZPZYMUUBXLUULYCYEXLYBGUBZYPUEZTZUUNXMHZYSPZPZFU
      DGUDZUULYCXLUUSGUDZFUDUULUUTXLUVAYTFXLUUNATZUUPYSPZPZGUDYTUVAUVCYTGACUVBU
      UPYRYSUVBUUOYQYBUUNAYPVGUTUNVHUUSUVDGUUSUUPUVBYSPZPUVDUURUVEUUPUUQUVBYSGA
      VIVJVKUUPUVBYSUPVEVLVMURUUSGFVNVMGFYBXMBVOVPVQUUBYOUULPUUMYOYTFVRYOYEUULK
      UHVSZYBUHHYOYEVTUHUHKRZUVFUHUHKMUVGWAUHUHKWBWCUHUHKWDWCEUIUHYBYDKUUKWEWFV
      JVEVMWGZYCYEVDVMURYLYHYJEUDYHYJEVREYIAYDWHWIWJWKEYDKXNUUKWLVMWMKXNWNWOXMB
      WPWQXNBXOWRWSXLYBYDXOUCZEWTZDXAXQXLUVJDXLUVJYKEWTYJYHEEYIXBXCXLUVIYKEXLEX
      DXLYMYKUVIUVHYBYDKXNUUKXEVMXFXGXHEDXOXIWSXJXNBXOXKWS $.
      $( [28-Mar-2008] $) $( [27-Mar-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y D $.  $d x y z F $.
    $d z G $.
    curry1.1 $e |- G = ( F o. `' ( 2nd |` ( { C } X. V ) ) ) $.
    $( Composition with ` ``' ( 2nd |`` ( { C } X. V ) ) ` turns any binary
       operation ` F ` with a constant first operand into a function ` G ` of
       the second operand only.  This transformation is called "currying." $)
    curry1 $p |- ( ( F Fn ( A X. B ) /\ C e. A ) ->
                 G = { <. x , y >. | ( x e. B /\ y = ( C F x ) ) } ) $=
      ( vz cxp wfn wcel wa cv co wceq copab cfv wral c2nd csn cvv cres ccnv
      cop wf1o f1ocnvfv 2ndconst adantr snidg visset jctir opelxp sylibr jca
      fvres syl op2ndg mpan2 eqtrd sylc fveq2d adantll df-opr syl6eqr ccom
      wfun fvco2 mp3an3 fnfun f1o4 sylib pm3.27d syl2an fveq1i syl5eq opreq2
      eqid oprex fvopab4 adantl 3eqtr4d r19.21aiva jctil wb cdm funco wfo f1o3
      pm3.27bi cima fndm imaeq2d cin crn wss snssi df-ss xpeq1 inxp incom inv1
      eqtr xpeq2 ax-mp reseq2 rneqd f1ofo forn imacnvcnv df-ima resres rneqi
      3eqtr dmco2 fneq1 df-fn bitr fnopab2 eqfnfv mpbird ) FCDJZKZECLZMZGANZDLB
      NEYFFOZPMABQZPZDDPZINZGRZYKYHRZPZIDSZMZYEYOYJYEYNIDYEYKDLZMZYKTEUAZUBJZUC
      ZUDZRZFRZEYKFOZYLYMYRUUDEYKUEZFRZUUEYDYQUUDUUGPYCYDYQMZUUCUUFFYTUBUUAUFZU
      UFYTLZMUUFUUARZYKPZUUCUUFPUUHYTUBUUFYKUUAUGUUHUUIUUJYDUUIYQEUBCUHZUIYDUUJ
      YQYDEYSLZYKUBLZMUUJYDUUNUUOECUJIUKZULEYKYSUBUUPUMUNZUIUOYDUULYQYDUUKUUFTR
      ZYKYDUUJUUKUURPUUQUUFYTTUPUQYDUUOUURYKPUUPEYKCUBURUSUTUIVAVBVCEYKFVDVEYRY
      KFUUBVFZRZUUDYLYEUUTUUDPZYQFVGZUUBUBKZUVAYCYDUVBUVCUUOUVAUUPUBYKFUUBVHVIY
      BFVJZYDUUAYTKZUVCYDUUIUVEUVCMUUMYTUBUUAVKVLVMVNUIYKGUUSHVOVPYQYMUUEPYEABY
      KYGUUEDYHYFYKEFVQYHVRZEYKFVSVTWAWBWCDVRWDYEGDKZYIYPWEZYEUUSVGZUUSWFZDPZMZ
      UVGYEUVIUVKUVBUUBVGZUVIYCYDFUUBWGUVDYDUUIUVMUUMUUIYTUBUUAWHUVMYTUBUUAWIWJ
      UQVNYEUUBUDZFWFZWKZDUVJYEUVPUVNYBWKZDYEUVOYBUVNYCUVOYBPYDYBFWLUIWMYDUVQDP
      YCYDTYTYBWNZUCZWOZDUVQYDUVTTYSDJZUCZWOZDYDUVSUWBYDUVRUWAPUVSUWBPYDYSCWNZD
      JZUWAUVRYDUWDYSPZUWEUWAPYDYSCWPUWFECWQYSCWRVLUWDYSDWSUQUVRUWDUBDWNZJZUWEY
      SUBCDWTUWGDPUWHUWEPUWGDUBWNDUBDXADXBXCUWGDUWDXDXEXCVPUVRUWATXFUQXGYDUWADU
      WBWHZUWCDPYDUWADUWBUFUWIEDCUHUWADUWBXHUQUWADUWBXIUQUTUVQUUAYBWKUUAYBUCZWO
      UVTUUAYBXJUUAYBXKUWJUVSTYTYBXLXMXNVPWAUTFUUBXOVPUOUVGUUSDKZUVLGUUSPUVGUWK
      WEHDGUUSXPXEUUSDXQXRUNUVGYHDKUVHABDYGYHEYFFVSUVFXSIDDGYHXTUSUQYA $.
      $( [28-Mar-2008] $) $( [28-Mar-2008] $)

    $( The value of a curried function with a constant first argument. $)
    curry1f $p |- ( ( F : ( A X. B ) --> D /\ C e. A ) ->
                 G : B --> D ) $=
      ( vx vy cxp wf wcel wa cv co wceq copab wral foprrn 3expa r19.21aiva
      eqid fopab2 sylib wfn curry1 ffn sylan feq1d mpbird ) ABJZDEKZCALZMZBDFKB
      DHNZBLZINCUOEOZPMHIQZKZUNUQDLZHBRUSUNUTHBULUMUPUTCUODABESTUAHIBDUQURURUBU
      CUDUNBDFUREUKUEUMFURPULHIABCEFGUFUKDEUGUHUIUJ $.
      $( [?] $) $( [29-Mar-2008] $)

    $( The value of a curried function with a constant first argument. $)
    curry1val $p |- ( ( F Fn ( A X. B ) /\ C e. A /\ D e. U ) ->
        ( G ` D ) = ( C F D ) ) $=
      ( vx vy cxp wfn wcel w3a cfv cv co wceq wa copab curry1 fveq1d 3adant3
      wn c0 eqid fvopab4ndm 3ad2ant3 cdm ndmoprg fndm id pm3.27 con3i syl3an
      eqtr4d 3expia opreq2 oprex fvopab4 pm2.61d2 3adant2 eqtrd ) FABKZLZCAMZDE
      MZNDGOZDIPZBMJPCVIFQZRZSIJTZOZCDFQZVEVFVHVMRVGVEVFSDGVLIJABCFGHUAUBUCVEVG
      VMVNRZVFVEVGSDBMZVOVEVGVPUDZVOVEVGVQNVMUEVNVQVEVMUERVGVKIJBDVLVLUFZUGUHFU
      IVDRVGVFVPSZUDVNUERVEVGVQCDEABFUJVDFUKVGULVSVPVFVPUMUNUOUPUQIJDVJVNBVLVID
      CFURVRCDFUSUTVAVBVC $.
      $( [?] $) $( [28-Mar-2008] $)
  $}

  $( Composition of the first member function with another function. $)
  1stcof $p |- ( F : A --> ( B X. C ) -> ( 1st o. F ) : A --> B ) $=
    ( cxp wf c1st ccom wfn crn wss wa cvv ffn fnf sylib wfo fo1st fof ax-mp
    fnfco mpan syl cres frn ssres2 rnss 3syl f1stres a1i sstrd rnco syl5ss jca
    df-f sylibr ) ABCEZDFZGDHZAIZUSJZBKZLABUSFURUTVBURAMDFZUTURDAIVCAUQDNADOPGM
    IZVCUTMMGFZVDMMGQVERMMGSTMMGNTMAGDUAUBUCURGDJZUDZJZBVAURVHGUQUDZJZBURVFUQKV
    GVIKVHVJKAUQDUEVFUQGUFVGVIUGUHVJBKZURUQBVIFVKBCUIUQBVIUETUJUKGDULUMUNABUSUO
    UP $.
    $( [5-Nov-2007] $) $( [12-Oct-2007] $)

  $( Membership in a cross product.  This version requires no quantifiers or
     dummy variables.  See also ~ elxp4 . $)
  elxp6 $p |- ( A e. ( B X. C ) <-> ( A = <. ( 1st ` A ) , ( 2nd ` A ) >.
               /\ ( ( 1st ` A ) e. B /\ ( 2nd ` A ) e. C ) ) ) $=
    ( cxp wcel csn cdm cuni crn cop wceq wa c1st cfv c2nd elxp4 1stval 2ndval
    opeq12i eqeq2i eleq1i anbi12i bitr4 ) ABCDEAAFZGHZUDIHZJZKZUEBEZUFCEZLZLAAM
    NZAONZJZKZULBEZUMCEZLZLABCPUOUHURUKUNUGAULUEUMUFAQZARZSTUPUIUQUJULUEBUSUAUM
    UFCUTUAUBUBUC $.
    $( [14-Oct-2004] $) $( [9-Oct-2004] $)

  $( Membership in a cross product.  This version requires no quantifiers or
     dummy variables.  See also ~ elxp4 . $)
  elxp7 $p |- ( A e. ( B X. C ) <-> ( A e. ( V X. V )
               /\ ( ( 1st ` A ) e. B /\ ( 2nd ` A ) e. C ) ) ) $=
    ( cxp wcel c1st cfv c2nd cop wceq wa cvv elxp6 fvex pm3.2i mpbiran2 anbi1i
    bitr4 ) ABCDEAAFGZAHGZIJZSBETCEKZKALLDEZUBKABCMUCUAUBUCUASLEZTLEZKALLMUDUEA
    FNAHNOPQR $.
    $( [19-Aug-2006] $) $( [19-Aug-2006] $)

  ${
    eqop.1 $e |- C e. V $.
    $( Two ways to express equality with an ordered pair. $)
    eqop $p |- ( A e. ( V X. V ) -> ( A = <. B , C >. <->
               ( ( 1st ` A ) = B /\ ( 2nd ` A ) = C ) ) ) $=
      ( cvv cxp wcel cop wceq c1st cfv c2nd wa fveq2 op1stg sylan9eqr op2ndg
      mpan2 jca eleq1 biimpac opelxpex syl pm3.27 sylanc elxp6 pm3.26bi opeq12
      sylan9eq impbida ) AEEFZGZABCHZIZAJKZBIZALKZCIZMZBEGZUNUSULUNMZUTUNMUPURU
      NUTUOUMJKBAUMJNBCEOPUNUTUQUMLKZCAUMLNUTCEGVBCIDBCEEQRPSVAUMUKGZUTUNULVCAU
      MUKTUABCEEUBUCULUNUDUEULUSAUOUQHZUMULAVDIUOEGUQEGMAEEUFUGUOUQBCUHUIUJ $.
      $( [6-Sep-2007] $) $( [3-Sep-2007] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Representation of cross product based on ordered pair component
       functions. $)
    xp2 $p |- ( A X. B ) = { x e. ( V X. V ) | ( ( 1st ` x ) e. A /\
              ( 2nd ` x ) e. B ) } $=
      ( cxp cv cvv wcel c1st cfv c2nd wa cab crab elxp7 abbi2i df-rab eqtr4 )
      BCDZAEZFFDZGSHIBGSJICGKZKZALUAATMUBARSBCNOUAATPQ $.
      $( [23-Oct-2006] $) $( [16-Sep-2006] $)
  $}

  $( An ordered pair theorem for members of cross products. $)
  xpopth $p |- ( ( A e. ( C X. D ) /\ B e. ( R X. S ) ) ->
       ( ( ( 1st ` A ) = ( 1st ` B ) /\
     ( 2nd ` A ) = ( 2nd ` B ) ) <-> A = B ) ) $=
    ( cxp wcel wa wceq c1st cfv c2nd cop elxp6 pm3.26bi eqeqan12d fvex opth
    syl6rbb ) ACDGHZBEFGHZIABJAKLZAMLZNZBKLZBMLZNZJUCUFJUDUGJIUAUBAUEBUHUAAUEJU
    CCHUDDHIACDOPUBBUHJUFEHUGFHIBEFOPQUCUDUFUGAKRAMRBMRST $.
    $( [1-Jul-2007] $) $( [20-Jun-2007] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( The membership relation for a cross product is inherited by union. $)
    unielxp $p |- ( A e. ( B X. C ) -> U. A e. U. ( B X. C ) ) $=
      ( vx cxp wcel cvv c1st cfv c2nd wa cuni elxp7 elvvuni adantr cv cab wex
      simprl wceq eleq2 eleq1 fveq2 eleq1d anbi12d cla4egv mpcom eluniab
      sylibr crab xp2 df-rab eqtr unieqi syl6eleqr mpancom sylbi ) ABCEZFAGGEZF
      ZAHIZBFZAJIZCFZKZKZALZURLZFZABCMVGAFZVFVIUTVJVEANOVJVFKZVGDPZUSFZVLHIZBFZ
      VLJIZCFZKZKZDQZLZVHVKVGVLFZVSKZDRZVGWAFUTVKWDVJUTVESWCVKDAUSVLATZWBVJVSVF
      VLAVGUAWEVMUTVRVEVLAUSUBWEVOVBVQVDWEVNVABVLAHUCUDWEVPVCCVLAJUCUDUEUEUEUFU
      GVSDVGUHUIURVTURVRDUSUJVTDBCUKVRDUSULUMUNUOUPUQ $.
      $( [14-Nov-2006] $) $( [16-Sep-2006] $)
  $}

  $( Reconstruction of a member of a relation in terms of its ordered pair
     components. $)
  1st2nd $p |- ( ( Rel B /\ A e. B ) ->
               A = <. ( 1st ` A ) , ( 2nd ` A ) >. ) $=
    ( wrel wcel wa cvv cxp c1st cfv c2nd cop wceq wss ssel2 df-rel sylanb
    elxp6 pm3.26bi syl ) BCZABDZEAFFGZDZAAHIZAJIZKLZBUBMUAUCTBUBANBOPUCUFUDFDUE
    FDEAFFQRS $.
    $( [30-Aug-2006] $) $( [29-Aug-2006] $)

  $( The first ordered pair component of a member of a relation belongs to
     the domain of the relation. $)
  1stdm $p |- ( ( Rel R /\ A e. R ) -> ( 1st ` A ) e. dom R ) $=
    ( wrel wcel wa c1st cfv cint cdm cvv cxp wceq wss df-rel biimp sseld imp
    1stval2 syl elreldm eqeltrd ) BCZABDZEZAFGZAHHZBIUDAJJKZDZUEUFLUBUCUHUBBUGA
    UBBUGMBNOPQARSBATUA $.
    $( [11-Jul-2007] $) $( [17-Sep-2006] $)

  $( The second ordered pair component of a member of a relation belongs to
     the range of the relation. $)
  2ndrn $p |- ( ( Rel R /\ A e. R ) -> ( 2nd ` A ) e. ran R ) $=
    ( wrel wcel wa c1st cfv c2nd cop crn 1st2nd pm3.27 eqeltrrd fvex opelrn
    syl ) BCZABDZEZAFGZAHGZIZBDUABJDSAUBBABKQRLMTUABAFNAHNOP $.
    $( [15-Aug-2007] $) $( [17-Sep-2006] $)

  $( Equality theorem for substitution of a class for an ordered pair (analog
     of ~ sbceq1a , that avoids the existential quantifiers of ~ copsexg ). $)
  sbcopeq1a $p |- ( <. x , y >. = A -> ( ph <->
                  [ ( 1st ` A ) / x ] [ ( 2nd ` A ) / y ] ph ) ) $=
    ( cv c2nd cfv wceq c1st wsbc wb cop sbceq1a sylan9bb fveq2 visset op2nd
    syl5eqr op1st sylanc ) CEZDFGZHZBEZDIGZHZAACUBJZBUEJZKUDUALZDHZUCAUGUFUHACU
    BMUGBUEMNUJUIFGUBUAUIDFOUDUABPZCPQRUJUIIGUEUDUIDIOUDUAUKSRT $.
    $( [19-Jul-2007] $) $( [19-Aug-2006] $)

  $( Equality theorem for substitution of a class ` A ` for an ordered pair
     ` <. x , y >. ` in ` B ` (analog of ~ csbeq1a ). $)
  csbopeq1a $p |- ( <. x , y >. = A -> B =
                  [_ ( 1st ` A ) / x ]_ [_ ( 2nd ` A ) / y ]_ B ) $=
    ( cv c2nd cfv wceq c1st csb cop csbeq1a sylan9eq fveq2 visset op2nd
    syl5eqr op1st sylanc ) BEZCFGZHZAEZCIGZHZDAUDBUADJZJZHUCTKZCHZUBUEDUFUGBUAD
    LAUDUFLMUIUHFGUATUHCFNUCTAOZBOPQUIUHIGUDUCUHCINUCTUJRQS $.
    $( [22-Aug-2006] $) $( [19-Aug-2006] $)

  ${
    $d z ph $.  $d x y z $.
    $( A way to define an ordered-pair class abstraction without using
       existential quantifiers. $)
    dfopab2 $p |- { <. x , y >. | ph } = { z | ( z e. ( V X. V ) /\
                  [ ( 1st ` z ) / x ] [ ( 2nd ` z ) / y ] ph ) } $=
      ( copab cv cop wceq wa wex cab cvv cxp wcel c2nd cfv wsbc c1st df-opab
      fvex hbsbc1v 19.41 wb fveq2 visset op2nd syl6req sbceq1a syl op1st bitrd
      pm5.32i exbii wal wi ax-17 hbsbcg ax-mp bitr elvv anbi1i 3bitr4 abbii
      eqtr ) ABCEDFZBFZCFZGZHZAIZCJZBJZDKVELLMNZACVEOPZQZBVERPZQZIZDKABCDSVLVRD
      VICJZVQIZBJVSBJZVQIVLVRVSVQBVOBVPVERTZUAUBVKVTBVKVIVQIZCJVTVJWCCVIAVQVIAV
      OVQVIVGVNHAVOUCVIVNVHOPVGVEVHOUDVFVGBUEZCUEUFUGACVNUHUIVIVFVPHVOVQUCVIVPV
      HRPVFVEVHRUDVFVGWDUJUGVOBVPUHUIUKULUMVIVQCVPLNVQVQCUNUOWBVOCBBVPLVFVPNCUP
      ACVNVEOTUAUQURUBUSUMVMWAVQBCVEUTVAVBVCVD $.
      $( [19-Oct-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d w ph $.  $d x y z w $.
    $( A way to define an operation class abstraction without using existential
       quantifiers. $)
    dfoprab3 $p |- { <. <. x , y >. , z >. | ph } = { <. w , z >. |
         ( w e. ( V X. V ) /\ [ ( 1st ` w ) / x ] [ ( 2nd ` w ) / y ] ph ) } $=
      ( copab2 cv cop wceq wa wex copab cvv cxp wcel c2nd cfv wsbc c1st
      dfoprab2 fvex hbsbc1v 19.41 wb fveq2 visset op2nd syl6req sbceq1a syl
      op1st bitrd pm5.32i exbii wal wi ax-17 hbsbcg ax-mp bitr elvv anbi1i
      3bitr4 opabbii eqtr ) ABCDFEGZBGZCGZHZIZAJZCKZBKZEDLVFMMNOZACVFPQZRZBVFSQ
      ZRZJZEDLABCDETVMVSEDVJCKZVRJZBKVTBKZVRJVMVSVTVRBVPBVQVFSUAZUBUCVLWABVLVJV
      RJZCKWAVKWDCVJAVRVJAVPVRVJVHVOIAVPUDVJVOVIPQVHVFVIPUEVGVHBUFZCUFUGUHACVOU
      IUJVJVGVQIVPVRUDVJVQVISQVGVFVISUEVGVHWEUKUHVPBVQUIUJULUMUNVJVRCVQMOVRVRCU
      OUPWCVPCBBVQMVGVQOCUQACVOVFPUAUBURUSUCUTUNVNWBVRBCVFVAVBVCVDVE $.
      $( [7-Sep-2006] $) $( [18-Aug-2006] $)
  $}

  ${
    $d w x y A $.  $d w x y B $.  $d w C $.  $d x y D $.  $d w x y z $.
    dfoprab5.1 $e |- ( <. x , y >. = w -> C = D ) $.
    $( A way to define an operation class abstraction without using existential
       quantifiers. $)
    dfoprab5 $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C )
           } = { <. w , z >. | ( w e. ( A X. B ) /\ z = D ) } $=
      ( cv wcel wa wceq copab2 cvv cxp c2nd cfv wsbc c1st copab dfoprab3 anass
      elxp7 anbi1i csb wb fvex sbcang ax-mp sbcel1gv ax-17 sbcgf anbi12i bitr
      a1i wi wal cop visset eqop eqcom 3bitr4g syl6bir 19.21aivv csbie2t syl
      eqeq2d sbceq2dig syl5bb anbi12d sbcbii pm5.32i 3bitr4r opabbii eqtr ) AJZ
      EKZBJZFKZLZCJZGMZLZABCNDJZOOPKZWDBWEQRZSZAWETRZSZLZDCUAWEEFPKZWBHMZLZDCUA
      WDABCDUBWKWNDCWFWIEKZWGFKZLZLZWMLWFWQWMLZLWNWKWFWQWMUCWLWRWMWEEFUDUEWFWJW
      SWFVRWPLZAWISZWBBWGGUFZMZAWISZLZWSWJWFXAWQXDWMXAWQUGWFXAVRAWISZWPAWISZLZW
      QWIOKZXAXHUGWETUHZVRWPAWIOUIUJXFWOXGWPXIXFWOUGXJAWIEOUKUJXIXGWPUGXJWPAWIO
      WPAULUMUJUNUOUPWFWBAWIXBUFZMZWMXDWFXKHWBWFVQWIMZVSWGMZLZGHMZUQZBURAURXKHM
      WFXQABWFXOVQVSUSZWEMZXPWFWEXRMWIVQMZWGVSMZLXSXOWEVQVSBUTVAXRWEVBXMXTXNYAV
      QWIVBVSWGVBUNVCIVDVEABWIWGGHXJWEQUHZVFVGVHXIXDXLUGXJAWIWBXBOVIUJVJVKWJWTX
      CLZAWISZXEXIWJYDUGXJWHYCAWIOWHWABWGSZWCBWGSZLZYCWGOKZWHYGUGYBWAWCBWGOUIUJ
      YEWTYFXCYEVRBWGSZVTBWGSZLZWTYHYEYKUGYBVRVTBWGOUIUJYIVRYJWPYHYIVRUGYBVRBWG
      OVRBULUMUJYHYJWPUGYBBWGFOUKUJUNUOYHYFXCUGYBBWGWBGOVIUJUNUOVLUJXIYDXEUGXJW
      TXCAWIOUIUJUOVJVMVNVOVP $.
      $( [10-Sep-2007] $) $( [3-Sep-2007] $)
  $}

  ${
    $d w x y A $.  $d w x y B $.  $d w C $.  $d x y D $.  $d w x y z $.
    dfoprab4.1 $e |- ( ( x = ( 1st ` w ) /\ y = ( 2nd ` w ) ) -> C = D ) $.
    $( A way to define an operation class abstraction without using existential
       quantifiers. $)
    dfoprab4 $p |- { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C )
           } = { <. w , z >. | ( w e. ( A X. B ) /\ z = D ) } $=
      ( cv c1st cfv wceq c2nd cop fveq2 visset op1st syl5eqr op2nd sylanc
      dfoprab5 ) ABCDEFGHAJZDJZKLZMBJZUDNLZMGHMUCUFOZUDMZIUIUHKLUEUCUHUDKPUCUFA
      QZRSUIUHNLUGUFUHUDNPUCUFUJBQTSUAUB $.
      $( [11-Sep-2007] $) $( [27-Aug-2007] $)
  $}

  ${
    $d x y A $.  $d x y ch $.
    elopabi.1 $e |- ( x = ( 1st ` A ) -> ( ph <-> ps ) ) $.
    elopabi.2 $e |- ( y = ( 2nd ` A ) -> ( ps <-> ch ) ) $.
    $( A consequence of membership in an ordered-pair class abstraction, using
       ordered pair extractors. $)
    elopabi $p |- ( A e. { <. x , y >. | ph } -> ch ) $=
      ( copab wcel c1st cfv c2nd cop wceq wrel relopab 1st2nd mpan eleq1 fvex
      opelopab syl6bb biimpcd mpd ) FADEIZJZFFKLZFMLZNZOZCUFPUGUKADEQFUFRSUKUGC
      UKUGUJUFJCFUJUFTABCDEUHUIFKUAFMUAGHUBUCUDUE $.
      $( [3-Sep-2006] $) $( [29-Aug-2006] $)
  $}

  ${
    $d w x y z A $.  $d x y ch $.  $d w ph $.  $d x ps $.  $d x y z th $.
    eloprabi.1 $e |- ( x = ( 1st ` ( 1st ` A ) ) -> ( ph <-> ps ) ) $.
    eloprabi.2 $e |- ( y = ( 2nd ` ( 1st ` A ) ) -> ( ps <-> ch ) ) $.
    eloprabi.3 $e |- ( z = ( 2nd ` A ) -> ( ch <-> th ) ) $.
    $( A consequence of membership in an operation class abstraction, using
       ordered pair extractors. $)
    eloprabi $p |- ( A e. { <. <. x , y >. , z >. | ph } -> th ) $=
      ( vw copab2 wcel c1st cfv c2nd cop wceq wrel reloprab 1st2nd mpan cv cvv
      cxp wsbc wa copab dfoprab3 eleq2i pm3.26 ssopab2i sseli eleq1 pm4.2i
      elopabi relxp 3syl sylbi opeq1d eqtrd wb fvex eloprabg mp3an syl6bb
      biimpcd mpd ) HAEFGMZNZHHOPZOPZVLQPZRZHQPZRZSZDVKHVLVPRZVQVJTVKHVSSAEFGUA
      HVJUBUCVKVLVOVPVKHLUDZUEUEUFZNZAFVTQPUGEVTOPUGZUHZLGUIZNZVLVOSZVJWEHAEFGL
      UJUKWFHWBLGUIZNVLWANZWGWEWHHWDWBLGWBWCULUMUNWBWIWILGHVTVLWAUOGUDVPSWIUPUQ
      WATWIWGUEUEURVLWAUBUCUSUTVAVBVRVKDVRVKVQVJNZDHVQVJUOVMUENVNUENVPUENWJDVCV
      LOVDVLQVDHQVDABCDEFGVMVNVPUEUEUEIJKVEVFVGVHVI $.
      $( [16-Nov-2006] $) $( [6-Nov-2006] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w z C $.  $d w x y z D $.
    foprab2.1 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Mapping of an operation class abstraction. $)
    foprab2 $p |- ( A. x e. A A. y e. B C e. D <-> F : ( A X. B ) --> D ) $=
      ( vw wcel wral cv c1st cfv c2nd csb cxp wf fvex ax-17 hbcsb1 hbel cvv
      wal wi hbcsbg ax-mp cop wceq csbopeq1a eqcoms eqcomd eleq1d ralxpf wa
      copab2 wsbc copab dfoprab3 anass elxp7 anbi1i wb sbcang sbcgf sbcel1gv
      anbi12i bitr sbceq2dig sbcbii 3bitr anbi2i 3bitr4 opabbii 3eqtr4 fopab2
      bitr3 ) FGKZBELADLAJMZNOZBVTPOZFQZQZGKZJDERZLWFGHSWEVSJABDEACCWDGACWAWCVT
      NTZCMZWAKZAUAUBWHGKZAUAUCBCCWDGWAUDKZWHWDKZWLBUEUFWGBACWAWCUDWIBUABCWBFVT
      PTZWHWBKBUAUBUGUHWJBUAUCVSJUAVTAMZBMZUIZUJZWDFGWQFWDFWDUJWPVTABVTFUKULUMU
      NUOJCWFGWDHWNDKZWOEKZUPZWHFUJZUPZABCUQVTUDUDRKZXBBWBURZAWAURZUPZJCUSHVTWF
      KZWHWDUJZUPZJCUSXBABCJUTIXIXFJCXCWADKZWBEKZUPZUPZXHUPXCXLXHUPZUPXIXFXCXLX
      HVAXGXMXHVTDEVBVCXEXNXCXEWRXKUPZWHWCUJZUPZAWAURZXOAWAURZXPAWAURZUPZXNWKXE
      XRVDWGXDXQAWAUDXDWTBWBURZXABWBURZUPZXQWBUDKZXDYDVDWMWTXABWBUDVEUHYBXOYCXP
      YBWRBWBURZWSBWBURZUPZXOYEYBYHVDWMWRWSBWBUDVEUHYFWRYGXKYEYFWRVDWMWRBWBUDWR
      BUAVFUHYEYGXKVDWMBWBEUDVGUHVHVIYEYCXPVDWMBWBWHFUDVJUHVHVIVKUHWKXRYAVDWGXO
      XPAWAUDVEUHXSXLXTXHXSWRAWAURZXKAWAURZUPZXLWKXSYKVDWGWRXKAWAUDVEUHYIXJYJXK
      WKYIXJVDWGAWADUDVGUHWKYJXKVDWGXKAWAUDXKAUAVFUHVHVIWKXTXHVDWGAWAWHWCUDVJUH
      VHVLVMVNVOVPVQVR $.
      $( [17-Sep-2006] $) $( [18-Aug-2006] $)

    ${
      foprab.2 $e |- ( ( x e. A /\ y e. B ) -> C e. D ) $.
      $( Mapping of an operation class abstraction. $)
      foprab $p |- F : ( A X. B ) --> D $=
        ( wcel wral cxp wf rgen2 foprab2 mpbi ) FGKZBELADLDEMGHNRABDEJOABCDEFGH
        IPQ $.
        $( [9-Aug-2007] $) $( [1-Jan-2007] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    fnoprab2g.1 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Functionality and domain of an operation class abstraction. $)
    fnoprab2g $p |- ( A. x e. A A. y e. B C e. V <-> F Fn ( A X. B ) ) $=
      ( cvv wcel wral cxp wf wfn foprab2 fnf bitr4 ) FIJBEKADKDELZIGMGRNABCDEFI
      GHORGPQ $.
      $( [4-Sep-2007] $) $( [29-Aug-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    fnoprab2.1 $e |- C e. V $.
    fnoprab2.2 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Functionality and domain of an operation class abstraction. $)
    fnoprab2 $p |- F Fn ( A X. B ) $=
      ( cvv wcel wral cxp wfn cv wa a1i rgen2 fnoprab2g mpbi ) FJKZBELADLGDEMNU
      AABDEUAAODKBOEKPHQRABCDEFGIST $.
      $( [26-Aug-1995] $)

    $( Domain of an operation class abstraction. $)
    dmoprab2 $p |- dom F = ( A X. B ) $=
      ( cxp wfn cdm wceq fnoprab2 fndm ax-mp ) GDEJZKGLQMABCDEFGHINQGOP $.
      $( [14-Nov-2004] $) $( [23-Sep-2004] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d z C $.  $d x y w v u D $.
    $d w v u F $.
    elrnoprabg.1 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Membership in the range of an operation class abstraction. $)
    elrnoprabg $p |- ( A. x e. A A. y e. B C e. R ->
                     ( D e. ran F <-> E. x e. A E. y e. B D = C ) ) $=
      ( vw vv vu wcel wral cxp wfn crn wceq wrex wb cvv elisset r19.20si
      fnoprab2g sylib cv cfv fvelrnb co hbra1 wa ax-17 hbral hban ra42 w3a
      oprabval4g eqeq1d eqcom syl6bb 3expia sylcom exp3a imp31 rexbida cop
      fveq2 df-opr syl6eqr rexxp copab2 hboprab1 hbxfr hbopr hbeq hbrex opreq1
      rexbidv cbvrex hboprab2 opreq2 rexbii 3bitr syl5bb sylan9bbr mpdan ) FHNZ
      BEOZADOZIDEPZQZGIRNZGFSZBETZADTZUAWJFUBNZBEOZADOWLWIWRADWHWQBEFHUCUDUDABC
      DEFIJUEUFWLWMKUGZIUHZGSZKWKTZWJWPKWKGIUIWJAUGZBUGZIUJZGSZBETZADTZWPXBWJXG
      WOADWIADUKWJXCDNZULXFWNBEWJXIBWIBADXIBUMZWHBEUKUNXJUOWJXIXDENZXFWNUAZWJXI
      XKXLWJXIXKULZWHXLWHABDEUPXIXKWHXLXIXKWHUQZXFFGSWNXNXEFGABCDEFHIJURUSFGUTV
      AVBVCVDVEVFVFXBLUGZMUGZIUJZGSZMETZLDTXCXPIUJZGSZMETZADTXHXAXRKLMDEWSXOXPV
      GZSZWTXQGYDWTYCIUHXQWSYCIVHXOXPIVIVJUSVKXSYBLADXRAMEXPENAUMAKKXQGAKXOXPIW
      SXONAUMAKIXMCUGFSULZABCVLZJYEABCKVMVNWSXPNZAUMVOWSGNZAUMVPVQYBLUMXOXCSZXR
      YAMEYIXQXTGXOXCXPIVRUSVSVTYBXGADYAXFMBEBKKXTGBKXCXPIWSXCNBUMBKIYFJYEABCKW
      AVNYGBUMVOYHBUMVPXFMUMXPXDSXTXEGXPXDXCIWBUSVTWCWDWEWFWG $.
      $( [4-Sep-2007] $) $( [27-Aug-2007] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d z C $.  $d x y w v u D $.
    $d w v u F $.
    elrnoprab.1 $e |- C e. V $.
    elrnoprab.2 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Membership in the range of an operation class abstraction. $)
    elrnoprab $p |- ( D e. ran F <-> E. x e. A E. y e. B D = C ) $=
      ( cvv wcel wral crn wceq wrex wb elrnoprabg cv a1i rgen mprg ) FKLZBEMZGH
      NLGFOBEPADPQADABCDEFGKHJRUDASDLUCBEUCBSELITUATUB $.
      $( [6-Aug-2004] $) $( [1-Aug-2004] $)
  $}

  ${
    $d x y z w v A $.  $d x y z B $.  $d x y z w v u $.
    $( An alternate possible definition of the ` 1st ` function. $)
    df1st2 $p |- { <. <. x , y >. , z >. | z = x } = ( 1st |` ( V X. V ) ) $=
      ( vu vw vv cv wceq copab2 c1st cvv cxp cres cfv wral wfn wa wb visset
      wcel pm3.2i biantrur oprabbii fnoprab2 wss wf wfo fo1st fof ax-mp ffn
      ssv fnssres mp2an eqfnfv eqid cop 1st2val opelxp mpbir2an fvres eqtr4
      a1i rgen2a fveq2 eqeq12d ralxp mpbir ) CGAGZHZABCIZJKKLZMZHZVLVLHZDGZVKNZ
      VPVMNZHZDVLOZVKVLPVMVLPZVNVOVTQRABCKKVIVKASZVJVIKTZBGKTZQZVJQABCWEVJWCWDW
      BBSUAUBUCUDJKPZVLKUEWAKKJUFZWFKKJUGWGUHKKJUIUJKKJUKUJVLULKVLJUMUNDVLVLVKV
      MUOUNVLUPVTEGZFGZUQZVKNZWJVMNZHZFKOEKOWMEFKWMWHKTZWIKTZQWKWJJNZWLABCWJURW
      JVLTZWLWPHWQWNWOWHWIKKFSZUSESWRUTWJVLJVAUJVBVCVDVSWMDEFKKVPWJHVQWKVRWLVPW
      JVKVEVPWJVMVEVFVGVHUT $.
      $( [14-Nov-2004] $) $( [14-Oct-2004] $)

    $( An alternate possible definition of the ` 2nd ` function. $)
    df2nd2 $p |- { <. <. x , y >. , z >. | z = y } = ( 2nd |` ( V X. V ) ) $=
      ( vu vw vv cv wceq copab2 c2nd cvv cxp cres cfv wral wfn wa wb visset
      wcel pm3.2i biantrur oprabbii fnoprab2 wss wf wfo fo2nd fof ax-mp ffn
      ssv fnssres mp2an eqfnfv eqid cop 2nd2val opelxp mpbir2an fvres eqtr4
      a1i rgen2a fveq2 eqeq12d ralxp mpbir ) CGBGZHZABCIZJKKLZMZHZVLVLHZDGZVKNZ
      VPVMNZHZDVLOZVKVLPVMVLPZVNVOVTQRABCKKVIVKBSZVJAGKTZVIKTZQZVJQABCWEVJWCWDA
      SWBUAUBUCUDJKPZVLKUEWAKKJUFZWFKKJUGWGUHKKJUIUJKKJUKUJVLULKVLJUMUNDVLVLVKV
      MUOUNVLUPVTEGZFGZUQZVKNZWJVMNZHZFKOEKOWMEFKWMWHKTZWIKTZQWKWJJNZWLABCWJURW
      JVLTZWLWPHWQWNWOWHWIKKFSZUSESWRUTWJVLJVAUJVBVCVDVSWMDEFKKVPWJHVQWKVRWLVPW
      JVKVEVPWJVMVEVFVGVHUT $.
      $( [6-Jul-2007] $) $( [10-Aug-2006] $)
  $}

  ${
    $d x y z A $. $( etc. $)
    $( Merge two functions in parallel.  Use as the second argument of a
       composition with a (2-place) operation to build compound operations
       such as ` z = ( ( sqr ` x ) + ( abs ` y ) ) ` . $)
    fpar $p |- ( ( F Fn A /\ G Fn B ) ->
         ( ( `' 1st o. ( F o. 1st ) ) i^i ( `' 2nd o. ( G o. 2nd ) ) ) =
         { <. <. x , y >. , z >. |
            ( ( x e. A /\ y e. B ) /\ z = <. ( F ` x ) , ( G ` y ) >. ) } ) $=
      (  ) ? $.
      $( [?] $) $( [17-Sep-2007] $)
  $}

  ${
    $d x y $.
    $( A function that can be used to feed a common value to both operands
       of an operation.  Use as the second argument of a composition with
       the function of ~ fpar in order to build compound functions such
       as ` y = ( ( sqr ` x ) + ( abs ` x ) ) ` . $)
    fsplit $p |- `' ( 1st |` I ) = { <. x , y >. | y = <. x , x >. } $=
      (  ) ? $.
      $( [?] $) $( [17-Sep-2007] $)
  $}


