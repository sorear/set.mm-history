$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinals (continued)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The class of all ordinal numbers is ordinal.  Proposition 7.12 of
       [TakeutiZaring] p. 38, but without using the Axiom of Regularity. $)
    ordon $p |- Ord On $=
      ( vx vy con0 word wtr cep wwe df-ord tron wfr cv wbr wceq w3o wral dfwe2
      onfr wcel wa ordtri3or epel biid 3orbi123i sylibr eloni syl2an rgen2a
      mpbir2an ) CDCECFGZCHIUICFJAKZBKZFLZUJUKMZUKUJFLZNZBCOACOABCFPQUOABCUJDZU
      KDZUOUJCRUKCRUPUQSUJUKRZUMUKUJRZNUOUJUKTULURUMUMUNUSABUAUMUBBAUAUCUDUJUEU
      KUEUFUGUHUH $.
      $( [17-May-1994] $)
  $}

  $( The epsilon relation well-orders the class of ordinal numbers.
     Proposition 4.8(g) of [Mendelson] p. 244. $)
  epweon $p |- _E We On $=
    ( con0 word cep wwe ordon ordwe ax-mp ) ABACDEAFG $.
    $( [1-Nov-2003] $) $( [1-Nov-2003] $)

  $( No set contains all ordinal numbers.  Proposition 7.13 of [TakeutiZaring]
     p. 38, but without using the Axiom of Regularity.  This is also known as
     the Burali-Forti paradox (remark in [Enderton] p. 194).  In 1897, Cesare
     Burali-Forti noticed that since the "set" of all ordinal numbers is an
     ordinal class ( ~ ordon ), it must be both an element of the set of all
     ordinal numbers yet greater than every such element.  ZF set theory
     resolves this paradox by not allowing the class of all ordinal numbers to
     be a set (so instead it is a proper class).  Here we prove the denial of
     its existence. $)
  onprc $p |- -. On e. _V $=
    ( con0 cvv wcel word wn ordon ordirr ax-mp elong mpbiri mto ) ABCZAACZADZME
    FAGHLMNFABIJK $.
    $( [18-May-1994] $)

  $( A way to express the ordinal property of a class in terms of the class of
     ordinal numbers.  Corollary 7.14 of [TakeutiZaring] p. 38 and its
     converse. $)
  ordeleqon $p |- ( Ord A <-> ( A e. On \/ A = On ) ) $=
    ( word con0 wcel wceq wo cvv onprc elisset mto w3o ordon ordtri3or mpan2
    df-3or sylib ord mt3i eloni ordeq mpbiri jaoi impbii ) ABZACDZACEZFZUDUGCAD
    ZUHCGDHCAIJUDUGUHUDUEUFUHKZUGUHFUDCBZUILACMNUEUFUHOPQRUEUDUFASUFUDUJLACTUAU
    BUC $.
    $( [1-Jun-2003] $)

  $( Any ordinal class is a subclass of the class of ordinal numbers.
     Corollary 7.15 of [TakeutiZaring] p. 38. $)
  ordsson $p |- ( Ord A -> A C_ On ) $=
    ( word con0 wcel wss wne wa wb ordon ordelssne mpan2 pm3.26 syl6bi wn wceq
    wo ordeleqon biimpi ord eqimss syl6 pm2.61d ) ABZACDZACEZUCUDUEACFZGZUEUCCB
    UDUGHIACJKUEUFLMUCUDNACOZUEUCUDUHUCUDUHPAQRSACTUAUB $.
    $( [18-May-1994] $)

  $( An ordinal number is a subset of the class of ordinal numbers. $)
  onss $p |- ( A e. On -> A C_ On ) $=
    ( con0 wcel word wss eloni ordsson syl ) ABCADABEAFAGH $.
    $( [5-Jun-1994] $)

  ${
    $d x y A $.
    $( The union of a class of ordinal numbers is ordinal.  Proposition 7.19 of
       [TakeutiZaring] p. 40. $)
    ssorduni $p |- ( A C_ On -> Ord U. A ) $=
      ( vx vy cuni wtr con0 wss word ordon trssord 3exp mpii cv wral wcel wrex
      wa wi ssel eloni ordtr trss 3syl syl6 anc2r syl ssuni syl8 r19.23adv
      eluni2 syl5ib r19.21aiv dftr3 sylibr ordelord ex visset elon syl6ibr
      ssrdv sylc ) ADZEZVBFGZVBHZAFGZVCVDFHZVEIVCVDVGVEVBFJKLVFBMZVBGZBVBNVCVFV
      IBVBVFVHCMZOZCAPZVIVHVBOZVFVKVICAVFVJAOZVKVHVJGZVNQZVIVFVNVKVORZRVNVKVPRR
      VFVNVJFOZVQAFVJSZVRVJHZVJEVQVJTZVJUAVJVHUBUCUDVNVKVOUEUFVHVJAUGUHUICVHAUJ
      ZUKULBVBUMUNVFBVBFVFVLVHFOZVMVFVKWCCAVFVNVRVKWCRZVSVRVTWDWAVTVKVHHZWCVTVK
      WEVJVHUOUPVHBUQURUSUFUDUIWBUKUTVA $.
      $( [30-May-1994] $)
  $}

  $( The union of a set of ordinal numbers is an ordinal number.  Theorem 9 of
     [Suppes] p. 132. $)
  ssonuni $p |- ( A e. B -> ( A C_ On -> U. A e. On ) ) $=
    ( wcel cuni con0 word wss cvv wb uniexg elong syl ssorduni syl5bir ) ABCZAD
    ZECZPFZAEGOPHCQRIABJPHKLAMN $.
    $( [3-Nov-2003] $) $( [1-Nov-2003] $)

  ${
    ssonuni.1 $e |- A e. _V $.
    $( The union of a set of ordinal numbers is an ordinal number.  Corollary
       7N(d) of [Enderton] p. 193. $)
    ssonunii $p |- ( A C_ On -> U. A e. On ) $=
      ( cvv wcel con0 wss cuni wi ssonuni ax-mp ) ACDAEFAGEDHBACIJ $.
      $( [20-Sep-2003] $)
  $}

  $( The union of an ordinal number is an ordinal number. $)
  onuni $p |- ( A e. On -> U. A e. On ) $=
    ( con0 wcel wss cuni onss ssonuni mpd ) ABCABDAEBCAFABGH $.
    $( [1-Oct-2006] $) $( [29-Sep-2006] $)

  $( The union of an ordinal class is ordinal. $)
  orduni $p |- ( Ord A -> Ord U. A ) $=
    ( word con0 wss cuni ordsson ssorduni syl ) ABACDAEBAFAGH $.
    $( [12-Sep-2003] $)

  ${
    $d x y z A $.
    $( The intersection (infimum) of a non-empty class of ordinal numbers
       belongs to the class.  Compare Exercise 4 of [TakeutiZaring] p. 45. $)
    onint $p |- ( ( A C_ On /\ A =/= (/) ) -> |^| A e. A ) $=
      ( vx vy vz con0 wss c0 wne cint wcel cv cin wceq wrex wa wi wn wral ssel
      ontri1 syl6bir ex sylan9 com4r imp31 r19.20dva disj visset elint2
      3imtr4g imdistani sylan2 exp32 com4l imp32 ssrdv intss1 ad2antrl eqssd
      eleq1d biimpd com34 pm2.43d r19.23adv word ordon tz7.5 mp3an1 syl5
      anabsi5 ) AEFZAGHZAIZAJZVKABKZLGMZBANZVNVKVLOVKVPVNBAVKVOAJZVPVNPVKVRVPVR
      VNVKVRVPVRVNPVKVRVPOOZVRVNVSVOVMAVSVOVMVSCVOVMVKVRVPCKZVOJZVTVMJZPWAVKVRV
      PWBWAVKVRVPWBPZWAVKVOEJZOZWCVKVROWAWEOZDKZVOJQZDARVTWGJZDARVPWBWFWHWIDAWA
      WEWGAJZWHWIPWEWJWHWAWIVKWJWGEJZWDWHWAWIPZPZAEWGSWDWKWMWDWKOWHVOWGFWLVOWGT
      VOWGVTSUAUBUCUDUEUFDAVOUGDVTACUHUIUJVKVRWDAEVOSUKULUMUNUOUPVRVMVOFVKVPVOA
      UQURUSUTVAUMVBVCVDEVEVKVLVQVFBEAVGVHVIVJ $.
      $( [18-Mar-2007] $) $( [31-Jan-1997] $)
  $}

  $( The intersection of a class of ordinal numbers is zero iff the class
     contains zero. $)
  onint0 $p |- ( A C_ On -> ( |^| A = (/) <-> (/) e. A ) ) $=
    ( con0 wss cint c0 wceq wcel wa wne onint cvv 0ex eleq1 mpbiri intex
    sylibr sylan2 wb adantl mpbid ex int0el impbid1 ) ABCZADZEFZEAGZUDUFUGUDUFH
    UEAGZUGUDAEIZUHUFAJUFUEKGZUIUFUJEKGLUEEKMNAOPQUFUHUGRUDUEEAMSTUAAUBUC $.
    $( [14-May-2004] $) $( [24-Apr-2004] $)

  ${
    $d x y A $.
    $( A non-empty class of ordinal numbers has a smallest member.  Exercise 9
       of [TakeutiZaring] p. 40. $)
    onssmin $p |- ( ( A C_ On /\ A =/= (/) ) -> E. x e. A A. y e. A x C_ y ) $=
      ( cint wcel cv wss wral wrex con0 c0 wne wa wceq sseq1 ralbidv rcla4ev
      onint intss1 rgen sylancl ) CDZCEUBBFZGZBCHZAFZUCGZBCHZACICJGCKLMUHUEAUBC
      UFUBNUGUDBCUFUBUCOPQCRUDBCUCCSTUA $.
      $( [30-May-2007] $) $( [3-Oct-2003] $)
  $}

  ${
    $d x y $.  $d y ph $.
    onminsb.1 $e |- ( ps -> A. x ps ) $.
    onminsb.2 $e |- ( x = |^| { x e. On | ph } -> ( ph <-> ps ) ) $.
    $( If a property is true for some ordinal number, it is true for a minimal
       ordinal number.  This version uses implicit substitution.  Theorem
       Schema 62 of [Suppes] p. 228. $)
    onminsb $p |- ( E. x e. On ph -> ps ) $=
      ( vy con0 wrex crab cint wcel c0 wne rabn0 wss ssrab2 onint mpan sylbir
      hbrab1 hbint cv ax-17 elrabf pm3.27bi syl ) ACGHZACGIZJZUHKZBUGUHLMZUJACG
      NUHGOUKUJACGPUHQRSUJUIGKBABCFUIGCFUHACFGTUAFUBGKCUCDEUDUEUF $.
      $( [3-Oct-2003] $) $( [3-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( If a property is true for some ordinal number, it is true for a minimal
       ordinal number.  This version uses explicit substitution.  Theorem
       Schema 62 of [Suppes] p. 228. $)
    onminesb $p |- ( E. x e. On ph -> [ |^| { x e. On | ph } / x ] ph ) $=
      ( vy con0 wrex crab cint wcel wsbc c0 wne rabn0 wss ssrab2 onint mpan
      sylbir cv ax-17 elrabsf pm3.27bi syl ) ABDEZABDFZGZUDHZABUEIZUCUDJKZUFABD
      LUDDMUHUFABDNUDOPQUFUEDHUGABCUEDCRDHBSTUAUB $.
      $( [29-Sep-2003] $)
  $}

  $( The intersection of a non-empty collection of ordinal numbers is an
     ordinal number.  Compare Exercise 6 of [TakeutiZaring] p. 44. $)
  oninton $p |- ( ( A C_ On /\ A =/= (/) ) -> |^| A e. On ) $=
    ( con0 wss c0 wne cint wcel onint ex ssel syld imp ) ABCZADEZAFZBGZMNOAGZPM
    NQAHIABOJKL $.
    $( [20-Mar-2007] $) $( [29-Jan-1997] $)

  $( The intersection of a class of ordinal numbers exists iff it is an ordinal
     number. $)
  onintrab $p |- ( |^| { x e. On | ph } e. _V <->
                 |^| { x e. On | ph } e. On ) $=
    ( con0 crab cint cvv wcel c0 wne intex wss ssrab2 oninton mpan sylbir
    elisset impbii ) ABCDZEZFGZSCGZTRHIZUARJRCKUBUAABCLRMNOSCPQ $.
    $( [6-Nov-2003] $) $( [6-Nov-2003] $)

  $( An existence condition equivalent to an intersection's being an ordinal
     number. $)
  onintrab2 $p |- ( E. x e. On ph <-> |^| { x e. On | ph } e. On ) $=
    ( con0 wrex crab cint cvv wcel intexrab onintrab bitri ) ABCDABCEFZGHLCHABC
    IABJK $.
    $( [6-Nov-2003] $) $( [6-Nov-2003] $)

  $( No member of a set of ordinal numbers belongs to its minimum. $)
  onnmin $p |- ( ( A C_ On /\ B e. A ) -> -. B e. |^| A ) $=
    ( con0 wss wcel wa cint wn intss1 adantl wb ontri1 c0 wne oninton ne0i
    sylan2 ssel2 sylanc mpbid ) ACDZBAEZFZAGZBDZBUDEHZUBUEUABAIJUDCEZBCEUEUFKUC
    UDBLUAAMNUGUBAOABPQACBRST $.
    $( [2-Feb-1997] $)

  ${
    $d x A $.  $d x ps $.
    onnminsb.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An ordinal number smaller than the minimum of a set of ordinal numbers
       does not have the property determining that set. ` ps ` is the wff
       resulting from the substitution of ` A ` for ` x ` in wff ` ph ` . $)
    onnminsb $p |- ( A e. On -> ( A e. |^| { x e. On | ph } -> -. ps ) ) $=
      ( con0 wcel crab cint wn wa elrab wss ssrab2 onnmin mpan sylbir ex con2d
      ) DFGZBDACFHZIGZTBUBJZTBKDUAGZUCABCDFELUAFMUDUCACFNUADOPQRS $.
      $( [9-Nov-2003] $) $( [9-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A way to show that an ordinal number equals the minimum of a non-empty
       collection of ordinal numbers: it must be in the collection, and it must
       not be larger than any member of the collection. $)
    oneqmin $p |- ( ( B C_ On /\ B =/= (/) ) -> ( A = |^| B <->
                  ( A e. B /\ A. x e. A -. x e. B ) ) ) $=
      ( con0 wss c0 wne wa cint wceq wcel cv wn wral eleq1 onint syl5cbir wi
      eleq2 biimpd onnmin ex con2d syl9r r19.21adv adantr jcad oneqmini impbid
      ) CDEZCFGZHZBCIZJZBCKZALZCKZMZABNZHZULUNUOUSUNUOUMCKULBUMCOCPQUJUNUSRUKUJ
      UNURABUNUPBKZUPUMKZUJURUNVAVBBUMUPSTUJUQVBUJUQVBMCUPUAUBUCUDUEUFUGUJUTUNR
      UKABCUHUFUI $.
      $( [10-May-2007] $) $( [14-Nov-2003] $)
  $}

  ${
    $d x y A $.
    bm2.5ii.1 $e |- A e. _V $.
    $( Problem 2.5(ii) of [BellMachover] p. 471. $)
    bm2.5ii $p |- ( A C_ On -> U. A = |^| { x e. On | A. y e. A y C_ x } ) $=
      ( con0 wss cuni wcel cv wral crab cint wceq ssonunii intmin wb unissb
      a1i rabbii inteqi syl5reqr syl ) CEFCGZEHZUCBIAIZFBCJZAEKZLZMCDNUDUCUEFZA
      EKZLUCUHAUCEOUJUGUIUFAEUIUFPUEEHBCUEQRSTUAUB $.
      $( [20-Sep-2003] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    onminex.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( If a wff is true for an ordinal number, there is a smallest ordinal
       number for which it is true. $)
    onminex $p |- ( E. x e. On ph -> E. x e. On ( ph /\ A. y e. x -. ps ) ) $=
      ( cv con0 wcel wa wex wn wral wrex cab c0 wne wi wal cint hbab1 hbint
      hbel ax-17 hbim hbal hban wceq eleq1 eleq2 imbi1d albidv anbi12d cla4egf
      anabsi5 wss ssab2 onint mpan oninton onelon ex syl visset elab onnmin
      sylbir con2d syli 19.21aiv sylanc abn0 abid bicomi df-ral anbi12i anass
      bitr3i exbii 3imtr3i df-rex 3imtr4i ) CFZGHZAIZCJZWCABKZDWBLZIZIZCJZACGMW
      HCGMWDCNZOPZWBWKHZDFZWBHZWFQZDRZIZCJZWEWJWKSZWKHZWNWTHZWFQZDRZWSWLXAXDWSW
      RXAXDICDWTWKCDWKWDCDTZUAZXAXDCCDDWTWKXFXEUBXCCDXBWFCXFWFCUCUDUEUFWBWTUGZW
      MXAWQXDWBWTWKUHXGWPXCDXGWOXBWFWBWTWNUIUJUKULUMUNWKGUOZWLXAACGUPZWKUQURWLX
      CDXBWLWNGHZWFWLWTGHZXBXJQXHWLXKXIWKUSURXKXBXJWTWNUTVAVBXJBXBXJBXBKZXJBIZW
      NWKHZXLWDXMCWNDVCWBWNUGWCXJABWBWNGUHEULVDXHXNXLXIWKWNVEURVFVAVGVHVIVJWDCV
      KWRWICWRWDWGIWIWDWMWGWQWMWDWDCVLVMWFDWBVNVOWCAWGVPVQVRVSACGVTWHCGVTWA $.
      $( [2-Feb-1997] $)
  $}

  $( The class of all ordinal numbers is its own successor. $)
  sucon $p |- suc On = On $=
    ( con0 cvv wcel wn csuc wceq onprc sucprc ax-mp ) ABCDAEAFGAHI $.
    $( [12-Sep-2003] $)

  $( A successor exists iff its class argument exists. $)
  sucexb $p |- ( A e. _V <-> suc A e. _V ) $=
    ( cvv wcel csn wa cun csuc unexb snex biantru df-suc eleq1i 3bitr4i ) ABCZA
    DZBCZEAOFZBCNAGZBCAOHPNAIJRQBAKLM $.
    $( [22-Jun-1998] $)

  $( The successor of a set is a set (generalization). $)
  sucexg $p |- ( A e. B -> suc A e. _V ) $=
    ( wcel cvv csuc elisset sucexb sylib ) ABCADCAEDCABFAGH $.
    $( [5-Jun-1994] $)

  ${
    sucex.1 $e |- A e. _V $.
    $( The successor of a set is a set. $)
    sucex $p |- suc A e. _V $=
      ( cvv wcel csuc sucexg ax-mp ) ACDAECDBACFG $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( The minimum of a class of ordinal numbers is less than the minimum of
       that class with its minimum removed. $)
    onmindif2 $p |- ( ( A C_ On /\ A =/= (/) ) ->
                   |^| A e. |^| ( A \ { |^| A } ) ) $=
      ( vx con0 wss c0 wne wa cint csn cdif wcel cv wral wn wceq wo onnmin
      adantlr wb ontri1 onsseleq bitr3d oninton adantr ssel2 sylanc mpbid ord
      eqcom syl6ib necon1ad expimpd eldifsn syl5ib r19.21aiv cvv intex elintg
      sylbi adantl mpbird ) ACDZAEFZGZAHZAVEIJZHKZVEBLZKZBVFMZVDVIBVFVDVHAKZVHV
      EFZGVIVHVFKVDVKVLVIVDVKGZVIVHVEVMVINVEVHOZVHVEOVMVIVNVMVHVEKNZVIVNPZVBVKV
      OVCAVHQRVECKZVHCKZVOVPSVMVQVRGVEVHDVOVPVEVHTVEVHUAUBVDVQVKAUCUDVBVKVRVCAC
      VHUERUFUGUHVEVHUIUJUKULVHAVEUMUNUOVCVGVJSZVBVCVEUPKVSAUQBVEVFUPURUSUTVA
      $.
      $( [28-May-2007] $) $( [20-Nov-2003] $)
  $}

  ${
    $d x A $.
    $( The successor of an ordinal number is an ordinal number.  Proposition
       7.24 of [TakeutiZaring] p. 41. $)
    suceloni $p |- ( A e. On -> suc A e. On ) $=
      ( vx con0 wcel csuc word wtr wss ordon trssord 3exp mpii cv wral csn wo
      onelss wi wceq elsn eqimss sylbi a1i orim12d cun df-suc eleq2i elun
      bitr2i oridm 3imtr3g sssucid sstr2 mpi syl6 r19.21aiv dftr3 sylibr wa
      onss snssi jca unss sylib syl5ss sylc cvv wb sucexg elong syl mpbird ) AC
      DZAEZCDZVNFZVNGZVNCHZVPVMVQVRCFZVPIVQVRVSVPVNCJKLVMBMZVNHZBVNNVQVMWABVNVM
      VTVNDZVTAHZWAVMVTADZVTAOZDZPZWCWCPWBWCVMWDWCWFWCAVTQWFWCRVMWFVTASWCBATVTA
      UAUBUCUDWBVTAWEUEZDWGVNWHVTAUFZUGVTAWEUHUIWCUJUKWCAVNHWAAULVTAVNUMUNUOUPB
      VNUQURVMWHCVNVMACHZWECHZUSWHCHVMWJWKAUTACVAVBAWECVCVDWIVEVFVMVNVGDVOVPVHA
      CVIVNVGVJVKVL $.
      $( [6-Jun-1994] $)
  $}

  $( The successor of an ordinal class is ordinal. $)
  ordsuc $p |- ( Ord A <-> Ord suc A ) $=
    ( cvv wcel word csuc wb con0 elong suceloni eloni syl syl6bir ordelord ex
    sucidg syl5com impbid wn wceq sucprc eqcomd ordeq pm2.61i ) ABCZADZAEZDZFZU
    DUEUGUDUEAGCZUGABHUIUFGCUGAIUFJKLUGAUFCZUEUDUGUJUEUFAMNABOPQUDRZAUFSUHUKUFA
    ATUAAUFUBKUC $.
    $( [3-Apr-1995] $)

  ${
    $d x A $.
    $( The collection of ordinals in the power class of an ordinal is its
       successor. $)
    ordpwsuc $p |- ( Ord A -> ( ~P A i^i On ) = suc A ) $=
      ( vx word cpw con0 cin csuc cv wcel wss wa wb ordsssuc expcom pm5.32d
      pm3.27 wi ordsuc ordelon ex sylbi ancrd impbid2 bitrd elin visset elpw
      anbi1i ancom 3bitri syl5bb eqrdv ) ACZBADZEFZAGZUMBHZEIZUQAJZKZUQUPIZUQUO
      IZUMUTURVAKZVAUMURUSVAURUMUSVALUQAMNOUMVCVAURVAPUMVAURUMUPCZVAURQARVDVAUR
      UPUQSTUAUBUCUDVBUQUNIZURKUSURKUTUQUNEUEVEUSURUQABUFUGUHUSURUIUJUKUL $.
      $( [31-Jan-2005] $) $( [30-Jan-2005] $)

    $( The collection of ordinal numbers in the power set of an ordinal number
       is its successor. $)
    onpwsuc $p |- ( A e. On -> ( ~P A i^i On ) = suc A ) $=
      ( con0 wcel word cpw cin csuc wceq eloni ordpwsuc syl ) ABCADAEBFAGHAIAJK
      $.
      $( [19-Oct-2004] $) $( [19-Oct-2004] $)
  $}

  $( The successor of an ordinal number is an ordinal number. $)
  sucelon $p |- ( A e. On <-> suc A e. On ) $=
    ( word cvv wcel wa csuc con0 ordsuc sucexb anbi12i elon2 3bitr4i ) ABZACDZE
    AFZBZOCDZEAGDOGDMPNQAHAIJAKOKL $.
    $( [9-Sep-2003] $)

  $( The successor of an element of an ordinal class is a subset of it. $)
  ordsucss $p |- ( Ord B -> ( A e. B -> suc A C_ B ) ) $=
    ( word wcel csuc wss wi wa wn ordnbtwn imnan sylibr adantr wb ordtri1
    ordsuc sylanb sylibrd ordelord sylan exp31 pm2.43b ) BCZABDZAEZBFZUDUCUDUFG
    ZUCUDUCUGACZUCUGUCUDHUHUCHUDBUEDZIZUFUHUDUJGZUCUHUDUIHIUKABJUDUIKLMUECUCUFU
    JNUHUEBOAPQRBASTUAUBUB $.
    $( [21-Jun-1998] $)

  $( A set belongs to an ordinal iff its successor is a subset of the ordinal.
     Exercise 8 of [TakeutiZaring] p. 42 and its converse. $)
  ordelsuc $p |- ( ( A e. C /\ Ord B ) -> ( A e. B <-> suc A C_ B ) ) $=
    ( wcel word wa csuc wss wi ordsucss adantl sucssel adantr impbid ) ACDZBEZF
    ABDZAGBHZPQRIOABJKORQIPABCLMN $.
    $( [29-Nov-2003] $) $( [29-Nov-2003] $)

  ${
    $d x A $.
    $( The successor of an ordinal number is the smallest larger ordinal
       number. $)
    onsucmin $p |- ( A e. On -> suc A = |^| { x e. On | A e. x } ) $=
      ( con0 wcel cv crab cint csuc wss word wb ordelsuc eloni sylan2 rabbidv
      inteqd wceq sucelon intmin sylbi eqtr2d ) BCDZBAEZDZACFZGBHZUCIZACFZGZUFU
      BUEUHUBUDUGACUBUCJUDUGKUCCDBUCCLUCMNOPUBUFCDUIUFQBRAUFCSTUA $.
      $( [8-Dec-2003] $) $( [28-Nov-2003] $)
  $}

  $( Membership is inherited by successors.  Generalization of Exercise 9 of
     [TakeutiZaring] p. 42. $)
  ordsucelsuc $p |- ( Ord B -> ( A e. B <-> suc A e. suc B ) ) $=
    ( word wcel csuc wi wa cvv wb wss wceq wo ordsseleq ordsuc sylanb adantl
    ordsucss ad2antll sucssel adantr impbid sucexb elsucg sylbi 3bitr4d ex wn
    elisset sylibr pm5.21ni a1d pm2.61i biimpd ordelord sylan exp31 pm2.43a
    pm2.43d biimprd ) BCZABDZAEZBEZDZUTVAVDVAUTVAVDFZUTVAUTVEACZUTVEUTVAGVFUTGZ
    VAVDAHDZVGVAVDIZFVHVGVIVHVGGZVBBJZVBBDVBBKLZVAVDVGVKVLIZVHVBCZUTVMVFVBBMANZ
    OPVJVAVKUTVAVKFVHVFABQRVHVKVAFVGABHSTUAVHVDVLIZVGVHVBHDZVPAUBZVBBHUCUDTUEUF
    VHUGVIVGVAVHVDABUHVDVQVHVBVCUHVRUIUJUKULZUMBAUNUOUPUQURUTVDVAVDUTVDVAFZUTVD
    UTVTVFUTVTUTVDGVGVAVDVSUSVCCZVDVFUTWAVDGVNVFVCVBUNVOUIBNOUOUPUQURUA $.
    $( [22-Jun-1998] $)

  $( The subclass relationship between two ordinal classes is inherited by
     their successors. $)
  ordsucsssuc $p |- ( ( Ord A /\ Ord B ) -> ( A C_ B <-> suc A C_ suc B ) ) $=
    ( word wa wcel wn csuc wss wb ordsucelsuc notbid adantr ordtri1 ordsuc
    syl2anb 3bitr4d ) ACZBCZDBAEZFZBGZAGZEZFZABHUBUAHZQTUDIRQSUCBAJKLABMUBCUACU
    EUDIQRUBUAMANBNOP $.
    $( [4-Oct-2003] $) $( [4-Oct-2003] $)

  ${
    $d x A $.  $d x B $.
    $( The successor of the maximum (i.e. union) of two ordinals is the maximum
       of their successors. $)
    ordsucun $p |- ( ( Ord A /\ Ord B ) ->
               suc ( A u. B ) = ( suc A u. suc B ) ) $=
      ( vx word wa cun csuc cv con0 wcel wb wo wss ordssun adantl ordsssuc
      ordun sylan2 adantrr adantrl orbi12d 3bitr3d elun syl6bbr expcom pm5.32d
      wi ordsuc ordelon ex sylbi syl pm4.71rd syl2anb 3bitr4d eqrdv ) ADZBDZEZC
      ABFZGZAGZBGZFZUSCHZIJZVEVAJZEVFVEVDJZEVGVHUSVFVGVHVFUSVGVHKVFUSEZVGVEVBJZ
      VEVCJZLZVHVIVEUTMZVEAMZVEBMZLZVGVLUSVMVPKVFVEABNOVFUTDZVMVGKUSVEUTPABQZRV
      IVNVJVOVKVFUQVNVJKURVEAPSVFURVOVKKUQVEBPTUAUBVEVBVCUCUDUEUFUSVGVFUSVQVGVF
      UGZVRVQVADZVSUTUHVTVGVFVAVEUIUJUKULUMUSVHVFVBDZVCDZVHVFUGZUQURWAWBEVDDZWC
      VBVCQWDVHVFVDVEUIUJULAUHBUHUNUMUOUP $.
      $( [7-Feb-2004] $) $( [28-Nov-2003] $)
  $}

  $( The maximum of two ordinals belongs to a third if each of them do. $)
  ordunel $p |- ( ( Ord A /\ B e. A /\ C e. A ) -> ( B u. C ) e. A ) $=
    ( word wcel w3a csuc wss wa cun ordsucss anim12d 3impib wceq ordsucun
    ordelord 3adant3 3adant2 sylanc sseq1d biimprd cvv wb ordelsuc unexg
    3adant1 3simp1 sylibrd unss syl5ib mpd ) ADZBAEZCAEZFZBGZAHZCGZAHZIZBCJZAEZ
    ULUMUNUTULUMUQUNUSBAKCAKLMUOUPURJZAHZVBUTUOVDVAGZAHZVBUOVFVDUOVEVCABDZCDZVE
    VCNUOBCOULUMVGUNABPQULUNVHUMACPRSTUAVAUBEZULVBVFUCUOVAAUBUDUMUNVIULBCAAUEUF
    ULUMUNUGSUHUPURAUIUJUK $.
    $( [19-Sep-2006] $) $( [18-Sep-2006] $)

  $( A class of ordinal numbers is a subclass of the successor of its union.
     Similar to Proposition 7.26 of [TakeutiZaring] p. 41. $)
  onsucuni $p |- ( A C_ On -> A C_ suc U. A ) $=
    ( con0 wss cuni word csuc ssorduni wa ssid ordunisssuc mpbii mpdan ) ABCZAD
    ZEZANFCZAGMOHNNCPNIANJKL $.
    $( [19-Sep-2003] $)

  $( An ordinal class is a subclass of the successor of its union. $)
  ordsucuni $p |- ( Ord A -> A C_ suc U. A ) $=
    ( word con0 wss cuni csuc ordsson onsucuni syl ) ABACDAAEFDAGAHI $.
    $( [12-Sep-2003] $)

  $( An ordinal class is either its union or the successor of its union. $)
  orduniorsuc $p |- ( Ord A -> ( A = U. A \/ A = suc U. A ) ) $=
    ( word cuni wceq csuc wne wss wa wn wcel orduniss wb orduni ordelssne
    mpancom biimprd mpand ordsucss syld ordsucuni jctild df-ne necom bitr3i
    eqss 3imtr4g orrd ) ABZAACZDZAUIEZDZUHUIAFZAUKGZUKAGZHUJIZULUHUMUOUNUHUMUIA
    JZUOUHUIAGZUMUQAKUHUQURUMHZUIBUHUQUSLAMUIANOPQUIARSATUAUPAUIFUMAUIUBAUIUCUD
    AUKUEUFUG $.
    $( [13-Sep-2003] $)

  ${
    $d x y A $.
    $( The class of all ordinal numbers is its own union.  Exercise 11 of
       [TakeutiZaring] p. 40. $)
    unon $p |- U. On = On $=
      ( vx vy con0 cuni cv wcel wrex eluni2 onelon r19.23aiva sylbi csuc
      elunii visset sucid suceloni sylancr impbii eqriv ) ACDZCAEZTFZUACFZUBUAB
      EZFZBCGUCBUACHUEUCBCUDUAIJKUAUALZFUFCFUBUCUAUFCMUAANOUAPQRS $.
      $( [12-Nov-2003] $) $( [12-Nov-2003] $)

    $( An ordinal class is equal to the union of its successor. $)
    ordunisuc $p |- ( Ord A -> U. suc A = A ) $=
      ( vx word con0 wcel wceq wo csuc cuni ordeleqon cv suceq unieqd id
      eqeq12d wtr eloni ordtr syl visset unisuc sylib vtoclga cvv wn onprc
      eleq1 mtbiri sucprc unon unieq 3eqtr4a eqtrd jaoi sylbi ) ACADEZADFZGAHZI
      ZAFZAJUPUTUQBKZHZIZVAFZUTBADVAAFZVCUSVAAVEVBURVAALMVENOVADEZVAPZVDVFVACVG
      VAQVARSVABTUAUBUCUQUSAIZAUQURAUQAUDEZUEURAFUQVIDUDEUFADUDUGUHAUISMUQDIDVH
      AUJADUKUQNULUMUNUO $.
      $( [24-Dec-2004] $) $( [10-Dec-2004] $)

    $( The union of the ordinal subsets of an ordinal number is that number. $)
    orduniss2 $p |- ( Ord A -> U. { x e. On | x C_ A } = A ) $=
      ( word cv wss con0 crab cuni csuc cpw cin ordpwsuc wcel wa cab df-rab
      inab incom eqtr3i df-pw eqcomi abid2 ineq12i 3eqtri syl5eq unieqd
      ordunisuc eqtrd ) BCZADZBEZAFGZHBIZHBUIULUMUIBJZFKZUMULBLULUJFMZUKNAOZUKA
      OZUPAOZKZUOUKAFPUSURKUQUTUPUKAQUSURRSURUNUSFUNURABTUAAFUBUCUDUEUFBUGUH $.
      $( [28-Feb-2005] $) $( [30-Jan-2005] $)
  $}

  $( A successor ordinal is the successor of its union. $)
  onsucuni2 $p |- ( ( A e. On /\ A = suc B ) -> suc U. A = A ) $=
    ( con0 wcel csuc wceq wa cuni wn eleq1 sucelon syl6bbr biimpac wi word
    eloni ordirr syl eleq2 sucidg syl5cbi mtod ordunisuc eqeq2d mtbird adantl
    wb id unieq eqeq12d adantr ex mpd orduniorsuc ord eqcomd ) ACDZABEZFZGZAAHZ
    EZUTAVAFZIZAVBFZUTBCDZVDUSUQVFUSUQURCDVFAURCJBKLMUSVFVDNUQUSVFVDUSVFGVCURUR
    HZFZVFVHIUSVFVHURBFZVFVIBBDZVFBOZVJIBPZBQRVIBURDVJVFURBBSBCTUAUBVFVGBURVFVK
    VGBFVLBUCRUDUEUFUSVCVHUGVFUSAURVAVGUSUHAURUIUJUKUEULUFUMUQVDVENZUSUQAOZVMAP
    VNVCVEAUNUORUKUMUP $.
    $( [1-Mar-2005] $) $( [10-Dec-2004] $)

  $( The successor of an ordinal class contains the empty set. $)
  0elsuc $p |- ( Ord A -> (/) e. suc A ) $=
    ( word csuc c0 wcel ordsuc wne nsuceq0 ord0eln0 mpbiri sylbi ) ABACZBZDLEZA
    FMNLDGAHLIJK $.
    $( [4-Apr-1995] $)

  $( The class of ordinal numbers is a limit ordinal. $)
  limon $p |- Lim On $=
    ( con0 wlim word c0 wne cuni wceq w3a ordon onn0 unon eqcomi 3pm3.2i
    df-lim mpbir ) ABACZADEZAAFZGZHPQSIJRAKLMANO $.
    $( [24-Mar-1995] $)

  ${
    onssi.1 $e |- A e. On $.
    $( An ordinal number is a subset of ` On ` . $)
    onssi $p |- A C_ On $=
      ( con0 wcel wss onss ax-mp ) ACDACEBAFG $.
      $( [11-Aug-1994] $)

    $( The successor of an ordinal number is an ordinal number.  Corollary
       7N(c) of [Enderton] p. 193. $)
    onsuci $p |- suc A e. On $=
      ( con0 wcel csuc suceloni ax-mp ) ACDAECDBAFG $.
      $( [12-Jun-1994] $)

    $( An ordinal number is either its own union (if zero or a limit ordinal)
       or the successor of its union. $)
    onuniorsuci $p |- ( A = U. A \/ A = suc U. A ) $=
      ( word cuni wceq csuc wo onordi orduniorsuc ax-mp ) ACAADZEAKFEGABHAIJ $.
      $( [13-Jun-1994] $)

    ${
      $d x A $.
      $( A limit ordinal is not a successor ordinal. $)
      onuninsuci $p |- ( A = U. A <-> -. E. x e. On A = suc x ) $=
        ( cv csuc wceq con0 wrex cuni wn wi wcel wa onirri id cun csn df-suc
        eqeq2i unieq sylbi uniun visset unisn uneq2i eqtri syl6eq wss eleq1
        mpbii wtr tron trsuc mpan word eloni ordtr syl df-tr sylib 3syl
        ssequn1 eqtrd sylan9eqr sucid eleq2 mpbiri adantr eqeltrd mto imnan
        mpbir a1i r19.23aiv suceq eqeq2d rcla4ev onuni ax-mp onuniorsuci ori
        sylancr impbii con2bii ) BADZEZFZAGHZBBIZFZWHWJJZWGWKAGWGWKKZWEGLZWLWGW
        JMZJWNBBLBCNWNBWEBWJWGBWIWEWJOWGWIWEIZWEPZWEWGWIWEWEQZPZIZWPWGBWRFWIWSF
        WFWRBWERSBWRTUAWSWOWQIZPWPWEWQUBWTWEWOWEAUCZUDUEUFUGWGWOWEUHZWPWEFWGWFG
        LZWMXBWGBGLZXCCBWFGUIUJGUKXCWMULGWEUMUNWMWEUKZXBWMWEUOXEWEUPWEUQURWEUSU
        TVAWOWEVBUTVCVDWGWEBLZWJWGXFWEWFLWEXAVEBWFWEVFVGVHVIVJWGWJVKVLVMVNWIGLZ
        BWIEZFZWHWKWGXIAWIGWEWIFWFXHBWEWIVOVPVQXDXGCBVRVSWJXIBCVTWAWBWCWD $.
        $( [21-Feb-2004] $) $( [18-Feb-2004] $)
    $}

    ${
      onsucssi.2 $e |- B e. On $.
      $( A set belongs to an ordinal number iff its successor is a subset of
         the ordinal number.  Exercise 8 of [TakeutiZaring] p. 42 and its
         converse. $)
      onsucssi $p |- ( A e. B <-> suc A C_ B ) $=
        ( con0 wcel word csuc wss wb onordi ordelsuc mp2an ) AEFBGABFAHBIJCBDKA
        BELM $.
        $( [16-Sep-1995] $)
    $}
  $}

  $( A successor is not a limit ordinal. $)
  nlimsucg $p |- ( A e. B -> -. Lim suc A ) $=
    ( wcel word csuc wlim wn cuni wceq wa ordsuc ordirr sylbi adantl wi eleq1
    pm3.27 sucidg adantr eqeltrd syl5cbir ordunisuc sylan2 mtod ex limuni a1i
    nsyld c0 wne w3a 3simp1 df-lim 3imtr4i con3i pm2.61d1 ) ABCZADZAEZFZGUQURUS
    USHZIZUTUQURVBGUQURJVBUSUSCZURVCGZUQURUSDZVDAKZUSLMNUQVAAIZVBVCOURVBVCVAUSC
    UQVGJZUSVAUSPVHVAAUSUQVGQUQAUSCVGABRSTUAAUBUCUDUEUTVBOUQUSUFUGUHUTURVEUSUIU
    JZVBUKVEUTURVEVIVBULUSUMVFUNUOUP $.
    $( [20-Sep-2006] $) $( [25-Mar-1995] $)

  ${
    $d x A $.
    $( An ordinal equal to its union is not a successor. $)
    orduninsuc $p |- ( Ord A -> ( A = U. A <-> -. E. x e. On A = suc x ) ) $=
      ( word con0 wcel wceq wo cuni cv csuc wrex wn wb ordeleqon c0 cif id
      unieq eqeq12d eqeq1 rexbidv notbid bibi12d 0elon elimel onuninsuci dedth
      unon eqcomi cvv onprc visset sucex eleq1 mpbiri mto a1i nrex 2th jaoi
      sylbi ) BCBDEZBDFZGBBHZFZBAIZJZFZADKZLZMZBNVBVKVCVBVKVBBOPZVLHZFZVLVGFZAD
      KZLZMBOBVLFZVEVNVJVQVRBVLVDVMVRQBVLRSVRVIVPVRVHVOADBVLVGTUAUBUCAVLBODUDUE
      UFUGVCVKDDHZFZDVGFZADKZLZMVTWCVSDUHUIWAADWALVFDEWADUJEZUKWAWDVGUJEVFAULUM
      DVGUJUNUOUPUQURUSVCVEVTVJWCVCBDVDVSVCQBDRSVCVIWBVCVHWAADBDVGTUAUBUCUOUTVA
      $.
      $( [21-Feb-2004] $) $( [18-Feb-2004] $)

    $( An ordinal equal to its union contains the successor of each of its
       members. $)
    ordunisuc2 $p |- ( Ord A -> ( A = U. A <-> A. x e. A suc x e. A ) ) $=
      ( word cuni wceq cv csuc con0 wrex wn wcel wral orduninsuc wi wa wo wb
      ordtri3 suceloni eloni syl sylan2 con2bid onnbtwn imnan sylibr con2d
      pm2.21 syl6 adantl ax-1 a1i jaod wss ordtri2or sylan ancoms orcom sylib
      adantr ordsssuc2 biimpd pm3.27 orim12d mpd ex impbid bitr3d pm5.74da
      ordelon ancrd impbid2 imbi1d impexp syl5bbr bitrd ralbidv2 ralnex ) BCZBB
      DEBAFZGZEZAHIJZWABKZABLZABMVSWBJZAHLWEWCVSWFWDAHBVSVTHKZWFNWGVTBKZWDNZNZW
      IVSWGWFWIVSWGOZBWAKZWDPZWFWIWKWBWMVSWACZWBWMJQWGBWARWGWAHKWNVTSWATUAUBUCW
      KWMWIWKWLWIWDWGWLWINVSWGWLWHJWIWGWHWLWGWHWLOJWHWLJNVTBUDWHWLUEUFUGWHWDUHU
      IUJWDWINWKWDWHUKULUMWKWIWMWKWIOZBVTUNZWHPZWMWKWQWIWKWHWPPZWQWGVSWRVTCVSWR
      WGVTBUOVTTUPUQWHWPURUSUTWOWPWLWHWDWKWPWLNWIWKWPWLBVTVAVBUTWKWIVCVDVEVFVGV
      HVIVSWGWHOZWDNWIWJVSWSWHWDVSWSWHWGWHVCVSWHWGVSWHWGBVTVJVFVKVLVMWGWHWDVNVO
      VPVQWBAHVRVOVP $.
      $( [4-Feb-2005] $) $( [1-Feb-2005] $)

    $( An ordinal is zero, a successor ordinal, or a limit ordinal. $)
    ordzsl $p |- ( Ord A <->
                  ( A = (/) \/ E. x e. On A = suc x \/ Lim A ) ) $=
      ( word c0 wceq cv csuc con0 wrex wlim w3o wo wn cuni orduninsuc biimprd
      unizlim sylibd orrd 3orass or12 bitri sylibr ord0 ordeq mpbiri wcel
      eleq1 suceloni syl5bir eloni syl6com r19.23aiv limord 3jaoi impbii ) BCZB
      DEZBAFZGZEZAHIZBJZKZUQVBURVCLZLZVDUQVBVEUQVBMZBBNEZVEUQVHVGABOPBQRSVDURVB
      VCLLVFURVBVCTURVBVCUAUBUCURUQVBVCURUQDCUDBDUEUFVAUQAHVAUSHUGZBHUGZUQVAVJU
      THUGVIBUTHUHUSUIUJBUKULUMBUNUOUP $.
      $( [2-Oct-2003] $) $( [1-Oct-2003] $)

    $( An ordinal number is zero, a successor ordinal, or a limit ordinal
       number. $)
    onzsl $p |- ( A e. On <->
               ( A = (/) \/ E. x e. On A = suc x \/ ( A e. _V /\ Lim A ) ) ) $=
      ( con0 wcel cvv wa word c0 wceq cv csuc wrex wlim w3o elisset pm4.71ri
      elong pm5.32i wo andi 0ex eleq1 mpbiri wi visset sucex a1i r19.23aiv
      jaoi orbi1i bitr4i ordzsl df-3or bitri anbi2i 3bitr4i 3bitri ) BCDZBEDZUR
      FUSBGZFZBHIZBAJZKZIZACLZUSBMZFZNZURUSBCOPUSURUTBEQRUSVBVFSZVGSZFZVJVHSZVA
      VIVLUSVJFZVHSVMUSVJVGTVJVNVHVJUSVBUSVFVBUSHEDUABHEUBUCVEUSACVEUSUDVCCDVEU
      SVDEDVCAUEUFBVDEUBUCUGUHUIPUJUKUTVKUSUTVBVFVGNVKABULVBVFVGUMUNUOVBVFVHUMU
      PUQ $.
      $( [5-Jan-2004] $) $( [1-Oct-2003] $)

    $( An alternate definition of a limit ordinal, which is any ordinal that is
       neither zero nor a successor. $)
    dflim3 $p |- ( Lim A <->
                 ( Ord A /\ -. ( A = (/) \/ E. x e. On A = suc x ) ) ) $=
      ( wlim word c0 wceq cv csuc con0 wrex wo wn wa limord ioran nlim0 limeq
      mtbiri con2i cuni limuni wb orduninsuc syl mpbid sylanbrc jca w3o ordzsl
      biimpi df-3or sylib orcanai impbii ) BCZBDZBEFZBAGHFAIJZKZLZMUOUPUTBNZUTU
      QLURLZUOUQUROUQUOUQUOECPBEQRSUOBBTFZVBBUAUOUPVCVBUBVAABUCUDUEUFUGUPUSUOUP
      UQURUOUHZUSUOKUPVDABUIUJUQURUOUKULUMUN $.
      $( [3-Nov-2004] $) $( [1-Nov-2004] $)

    $( An alternate definition of a limit ordinal. $)
    dflim4 $p |- ( Lim A <->
                 ( Ord A /\ (/) e. A /\ A. x e. A suc x e. A ) ) $=
      ( wlim word c0 wcel cuni wceq w3a cv csuc wral dflim2 wa ordunisuc2
      anbi2d pm5.32i 3anass 3bitr4i bitri ) BCBDZEBFZBBGHZIZUAUBAJKBFABLZIZBMUA
      UBUCNZNUAUBUENZNUDUFUAUGUHUAUCUEUBABOPQUAUBUCRUAUBUERST $.
      $( [4-Feb-2005] $) $( [1-Feb-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The successor of a member of a limit ordinal is also a member. $)
    limsuc $p |- ( Lim A -> ( B e. A <-> suc B e. A ) ) $=
      ( vx wlim wcel csuc word c0 cv wral w3a wi dflim4 wceq suceq eleq1d
      rcla4cv 3ad2ant3 sylbi wtr limord ordtr trsuc ex 3syl impbid ) ADZBAEZBFZ
      AEZUGAGZHAEZCIZFZAEZCAJZKUHUJLZCAMUPUKUQULUOUJCBAUMBNUNUIAUMBOPQRSUGUKATZ
      UJUHLAUAAUBURUJUHABUCUDUEUF $.
      $( [3-Sep-2003] $)

    $( A class includes a limit ordinal iff the successor of the class includes
       it. $)
    limsssuc $p |- ( Lim A -> ( A C_ B <-> A C_ suc B ) ) $=
      ( vx wlim wss csuc sssucid sstr2 mpi wa cv wcel wceq wn wi eleq1 biimpcd
      limsuc biimpa word wb ordtri1 limord adantr ordelord sylan ordsuc sylib
      sylanc con2bid mpbid ex sylan9r con2d com23 imp31 wo ssel2 visset elsuc
      ord con1d adantll mpd ssrdv impbid2 ) ADZABEZABFZEZVHBVIEVJBGABVIHIVGVJVH
      VGVJJZCABVKCKZALZVLBLZVKVMJVLBMZNZVNVGVJVMVPVGVMVJVPVGVMVJVPOVGVMJVOVJVMV
      OBALZVGVJNZVOVMVQVLBAPQVGVQVRVGVQJZVIALZVRVGVQVTABRSVSVJVTATZVITZVJVTNUAV
      SAVIUBVGWAVQAUCZUDVSBTZWBWAVQWDVGABUEWCUFBUGUHUIUJUKULUMUNULUOUPVJVMVPVNO
      VGVJVMJZVNVOWEVNVOWEVLVILVNVOUQAVIVLURVLBCUSUTUHVAVBVCVDULVEULVF $.
      $( [6-Nov-2003] $) $( [30-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( Two ways to express the class of non-limit ordinal numbers.  Part of
       Definition 7.27 of [TakeutiZaring] p. 42, who use the symbol K_I for
       this class. $)
    nlimon $p |- { x e. On | ( x = (/) \/ E. y e. On x = suc y ) } =
                   { x e. On | -. Lim x } $=
      ( cv c0 wceq csuc con0 wrex wo wlim wn wcel word wb eloni dflim3 baib
      con2bid syl rabbii ) ACZDEUABCFEBGHIZUAJZKZAGUAGLUAMZUBUDNUAOUEUCUBUCUEUB
      KBUAPQRST $.
      $( [4-Nov-2004] $) $( [1-Nov-2004] $)
  $}

  ${
    $d x y z A $.
    $( The union of a nonempty class of limit ordinals is a limit ordinal. $)
    limuni3 $p |- ( ( A =/= (/) /\ A. x e. A Lim x ) -> Lim U. A ) $=
      ( vy vz c0 wne cv wlim wral wa cuni word wcel csuc w3a con0 wss limeq
      rcla4v cvv visset limelon mpan syl6com ssrdv ssorduni syl adantl wex wi
      n0 elunii expcom 0ellim syl5 syld 19.23aiv sylbi imp wrex rcla4cv limsuc
      anbi1d syl6bi exp3a com3r sylcom r19.23adv eluni2 syl5ib r19.21aiv 3jca
      dflim4 sylibr ) BEFZAGZHZABIZJZBKZLZEVTMZCGZNZVTMZCVTIZOVTHVSWAWBWFVRWAVO
      VRBPQWAVRDBPDGZBMZVRWGHZWGPMZVQWIAWGBVPWGRZSZWGTMWIWJDUAWGTUBUCUDUEBUFUGU
      HVOVRWBVOWHDUIVRWBUJZDBUKWHWMDWHVRWIWBWLWHEWGMZWBWIWNWHWBEWGBULUMWGUNUOUP
      UQURUSVRWFVOVRWECVTVRWCWGMZDBUTWEWCVTMVRWOWEDBVRWHWIWOWEUJVQWIAWGBWKVAWIW
      OWHWEWIWOWHWEWIWOWHJWDWGMZWHJWEWIWOWPWHWGWCVBVCWDWGBULVDVEVFVGVHDWCBVIVJV
      KUHVLCVTVMVN $.
      $( [29-May-2007] $) $( [1-Feb-2005] $)
  $}


