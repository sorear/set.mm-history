$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Natural number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Addition with zero.  Theorem 4I(A1) of [Enderton] p. 79. $)
  nna0 $p |- ( A e. om -> ( A +o (/) ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnon oa0 syl ) ABCADCAEFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Theorem 4J(A1) of [Enderton] p. 80. $)
  nnm0 $p |- ( A e. om -> ( A .o (/) ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnon om0 syl ) ABCADCAEFGEHAIAJK $.
    $( [20-Sep-1995] $)

  $( Addition with successor.  Theorem 4I(A2) of [Enderton] p. 79. $)
  nnasuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A +o suc B ) = suc ( A +o B ) ) $=
    ( com wcel con0 csuc coa co wceq nnon oasuc syl2an ) ACDAEDBEDABFGHABGHFIBC
    DAJBJABKL $.
    $( [20-Sep-1995] $)

  $( Multiplication with successor.  Theorem 4J(A2) of [Enderton] p. 80. $)
  nnmsuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A .o suc B ) = ( ( A .o B ) +o A ) ) $=
    ( com wcel con0 csuc comu co coa wceq nnon omsuc syl2an ) ACDAEDBEDABFGHABG
    HAIHJBCDAKBKABLM $.
    $( [20-Sep-1995] $)

  $( Addition to zero.  Remark in proof of Theorem 4K(2) of [Enderton]
     p. 81. $)
  nna0r $p |- ( A e. om -> ( (/) +o A ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnon oa0r syl ) ABCADCEAFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Exercise 16 of [Enderton] p. 82. $)
  nnm0r $p |- ( A e. om -> ( (/) .o A ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnon om0r syl ) ABCADCEAFGEHAIAJK $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Closure of addition of natural numbers.  Proposition 8.9 of
       [TakeutiZaring] p. 59.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnacl $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) e. om ) $=
      ( vx vy com wcel coa co cv wi wceq opreq2 eleq1d imbi2d c0 csuc nna0
      ibir wa peano2 nnasuc syl5ibr expcom finds2 vtoclga impcom ) BEFAEFZABGHZ
      EFZUGACIZGHZEFZJUGUIJCBEUJBKZULUIUGUMUKUHEUJBAGLMNULAOGHZEFZADIZGHZEFZAUP
      PZGHZEFZUGCDUJOKUKUNEUJOAGLMUJUPKUKUQEUJUPAGLMUJUSKUKUTEUJUSAGLMUGUOUGUNA
      EAQMRUGUPEFZURVAJURVAUGVBSZUQPZEFUQTVCUTVDEAUPUAMUBUCUDUEUF $.
      $( [22-Oct-2011] $) $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure of multiplication of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnmcl $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) e. om ) $=
      ( vx vy com wcel comu co cv wi wceq opreq2 eleq1d imbi2d c0 csuc nnm0
      peano1 syl6eqel wa coa nnacl expcom adantr nnmsuc sylibrd finds2 vtoclga
      impcom ) BEFAEFZABGHZEFZUJACIZGHZEFZJUJULJCBEUMBKZUOULUJUPUNUKEUMBAGLMNUO
      AOGHZEFADIZGHZEFZAURPZGHZEFZUJCDUMOKUNUQEUMOAGLMUMURKUNUSEUMURAGLMUMVAKUN
      VBEUMVAAGLMUJUQOEAQRSUJUREFZUTVCJUJVDTZUTUSAUAHZEFZVCUJUTVGJVDUTUJVGUSAUB
      UCUDVEVBVFEAURUEMUFUCUGUHUI $.
      $( [22-Oct-2011] $) $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of exponentiation of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnecl $p |- ( ( A e. om /\ B e. om ) -> ( A ^o B ) e. om ) $=
      ( vy vx com wcel coe co cv wi wceq opreq2 eleq1d imbi2d c0 csuc c1o con0
      nnon oe0 syl df-1o peano1 peano2 ax-mp eqeltri syl6eqel wa comu nnmcl
      expcom adantr oesuc syl2an sylibrd finds2 vtoclga impcom ) BEFAEFZABGHZEF
      ZUSACIZGHZEFZJUSVAJCBEVBBKZVDVAUSVEVCUTEVBBAGLMNVDAOGHZEFADIZGHZEFZAVGPZG
      HZEFZUSCDVBOKVCVFEVBOAGLMVBVGKVCVHEVBVGAGLMVBVJKVCVKEVBVJAGLMUSVFQEUSARFZ
      VFQKASZATUAQOPZEUBOEFVOEFUCOUDUEUFUGUSVGEFZVIVLJUSVPUHZVIVHAUIHZEFZVLUSVI
      VSJVPVIUSVSVHAUJUKULVQVKVREUSVMVGRFVKVRKVPVNVGSAVGUMUNMUOUKUPUQUR $.
      $( [22-Oct-2011] $) $( [24-Mar-2007] $)
  $}

  $( Reverse closure law for addition of natural numbers.  Exercise 1 of
     [TakeutiZaring] p. 62 and its converse. $)
  nnarcl $p |- ( ( A e. On /\ B e. On ) ->
               ( ( A +o B ) e. om <-> ( A e. om /\ B e. om ) ) ) $=
    ( con0 wcel wa coa co com wss wi oaword1 word eloni ordom ordtr2 sylancl
    exp3a adantr mpd oaword2 ancoms adantl jcad nnacl impbid1 ) ACDZBCDZEZABFGZ
    HDZAHDZBHDZEUHUJUKULUHAUIIZUJUKJZABKUFUMUNJUGUFUMUJUKUFALHLZUMUJEUKJAMNAUIH
    OPQRSUHBUIIZUJULJZUGUFUPBATUAUGUPUQJUFUGUPUJULUGBLUOUPUJEULJBMNBUIHOPQUBSUC
    ABUDUE $.
    $( [12-Dec-2004] $)

  ${
    $d x A $.  $d x y z B $.
    $( Addition of natural numbers is commutative.  Theorem 4K(2) of [Enderton]
       p. 81. $)
    nnacom $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) = ( B +o A ) ) $=
      ( vx vy vz com wcel coa co wceq cv wi c0 csuc opreq1 opreq2 eqeq12d
      imbi2d nna0r nna0 eqtr4d wa suceq syl peano2b sylbi con0 nnon suceloni
      oasuc syl2an anim12i 3syl syl5ibr expcom a2d finds imp nnasuc ) AFGBFGZAB
      HIZBAHIZJZUTCKZBHIZBVDHIZJZLUTMBHIZBMHIZJZLUTDKZBHIZBVKHIZJZLUTVKNZBHIZBV
      OHIZJZLUTVCLCDAVDMJZVGVJUTVSVEVHVFVIVDMBHOVDMBHPQRVDVKJZVGVNUTVTVEVLVFVMV
      DVKBHOVDVKBHPQRVDVOJZVGVRUTWAVEVPVFVQVDVOBHOVDVOBHPQRVDAJZVGVCUTWBVEVAVFV
      BVDABHOVDABHPQRUTVHBVIBSBTUAVKFGZUTVNVRUTWCVNVRLVNVRUTWCUBZVLNZVMNZJVLVMU
      CWDVPWEVQWFUTWCVPWEJZWCVOVDHIZVKVDHIZNZJZLWCVOMHIZVKMHIZNZJZLWCVOEKZHIZVK
      WPHIZNZJZLWCVOWPNZHIZVKXAHIZNZJZLWCWGLCEBVSWKWOWCVSWHWLWJWNVDMVOHPVSWIWMJ
      WJWNJVDMVKHPWIWMUCUDQRVDWPJZWKWTWCXFWHWQWJWSVDWPVOHPXFWIWRJWJWSJVDWPVKHPW
      IWRUCUDQRVDXAJZWKXEWCXGWHXBWJXDVDXAVOHPXGWIXCJWJXDJVDXAVKHPWIXCUCUDQRVDBJ
      ZWKWGWCXHWHVPWJWEVDBVOHPXHWIVLJWJWEJVDBVKHPWIVLUCUDQRWCWLVOWNWCVOFGWLVOJV
      KUEVOTUFWCWMVKJWNVOJVKTWMVKUCUDUAWPFGZWCWTXEWCXIWTXELWTXEWCXIUBZWQNZWSNZJ
      WQWSUCXJXBXKXDXLWCVOUGGZWPUGGZXBXKJXIWCVKUGGZXMVKUHZVKUIUDWPUHZVOWPUJUKXJ
      XOXNUBXCWSJXDXLJWCXOXIXNXPXQULVKWPUJXCWSUCUMQUNUOUPUQURBVKUSQUNUOUPUQUR
      $.
      $( [6-May-1995] $)
  $}

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers. $)
  nnaordi $p |- ( ( B e. om /\ C e. om ) ->
               ( A e. B -> ( C +o A ) e. ( C +o B ) ) ) $=
    ( com wcel con0 coa co wi nnon oaordi syl2an ) BDEBFECFEABECAGHCBGHEICDEBJC
    JABCKL $.
    $( [3-Feb-1996] $)

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers, and its converse. $)
  nnaord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( C +o A ) e. ( C +o B ) ) ) $=
    ( com wcel con0 coa co wb nnon oaord syl3an ) ADEAFEBDEBFECDECFEABECAGHCBGH
    EIAJBJCJABCKL $.
    $( [7-Mar-1996] $)

  $( Ordering property of addition of natural numbers. $)
  nnaordr $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( A +o C ) e. ( B +o C ) ) ) $=
    ( com wcel w3a coa co nnaord wceq nnacom ancoms 3adant2 3adant1 eleq12d
    bitrd ) ADEZBDEZCDEZFZABECAGHZCBGHZEACGHZBCGHZEABCITUAUCUBUDQSUAUCJZRSQUECA
    KLMRSUBUDJZQSRUFCBKLNOP $.
    $( [9-Nov-2002] $)

  $( Addition of natural numbers is associative.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/3
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case..)  Theorem 4K(1) of [Enderton] p. 81. $)
  nnaass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A +o B ) +o C ) = ( A +o ( B +o C ) ) ) $=
    ( com wcel con0 coa co wceq nnon oaass syl3an ) ADEAFEBDEBFECDECFEABGHCGHAB
    CGHGHIAJBJCJABCKL $.
    $( [20-Sep-1995] $)

  $( Distributive law for natural numbers.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/4
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case.)  Theorem 4K(3) of [Enderton] p. 81. $)
  nndi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( A .o ( B +o C ) ) = ( ( A .o B ) +o ( A .o C ) ) ) $=
    ( com wcel con0 coa co comu wceq nnon odi syl3an ) ADEAFEBDEBFECDECFEABCGHI
    HABIHACIHGHJAKBKCKABCLM $.
    $( [20-Sep-1995] $)

  $( Multiplication of natural numbers is associative.  (For brevity, this is
     just a special case of the proof for ordinals.  A direct proof would be
     about 1/3 the size of the ordinal proof, since it would use finite
     induction and not require the limit ordinal case..)  Theorem 4K(4) of
     [Enderton] p. 81. $)
  nnmass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A .o B ) .o C ) = ( A .o ( B .o C ) ) ) $=
    ( com wcel con0 comu co wceq nnon omass syl3an ) ADEAFEBDEBFECDECFEABGHCGHA
    BCGHGHIAJBJCJABCKL $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Multiplication with successor.  Exercise 16 of [Enderton] p. 82.  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    nnmsucr $p |- ( ( A e. om /\ B e. om ) ->
                  ( suc A .o B ) = ( ( A .o B ) +o B ) ) $=
      ( vx vy com wcel csuc comu co coa wceq cv wi opreq2 id opreq12d eqeq12d
      imbi2d c0 nnm0 peano1 nnmcl mpan2 nna0 syl peano2 3eqtr4rd wa opreq1
      peano2b nnmsuc sylanb opreq1d nnacom suceq nnasuc ancoms 3eqtr4d opreq2d
      nnaass syl3an3b syl3an1 3expb anidms an42s eqtrd syl5ibr expcom finds2
      vtoclga impcom ) BEFAEFZAGZBHIZABHIZBJIZKZVLVMCLZHIZAVRHIZVRJIZKZMVLVQMCB
      EVRBKZWBVQVLWCVSVNWAVPVRBVMHNWCVTVOVRBJVRBAHNWCOPQRWBVMSHIZASHIZSJIZKVMDL
      ZHIZAWGHIZWGJIZKZVMWGGZHIZAWLHIZWLJIZKZVLCDVRSKZVSWDWAWFVRSVMHNWQVTWEVRSJ
      VRSAHNWQOPQVRWGKZVSWHWAWJVRWGVMHNWRVTWIVRWGJVRWGAHNWROPQVRWLKZVSWMWAWOVRW
      LVMHNWSVTWNVRWLJVRWLAHNWSOPQVLWESWFWDATVLWEEFZWFWEKVLSEFWTUAASUBUCWEUDUEV
      LVMEFZWDSKAUFVMTUEUGVLWGEFZWKWPMWKWPVLXBUHZWHVMJIZWJVMJIZKWHWJVMJUIXCWMXD
      WOXEVLXAXBWMXDKAUJZVMWGUKULXCWOWIAJIZWLJIZXEXCWNXGWLJAWGUKUMXCWIAWLJIZJIZ
      WIWGVMJIZJIZXHXEXCXIXKWIJXCAWGJIZGZWGAJIZGZXIXKXCXMXOKXNXPKAWGUNXMXOUOUEA
      WGUPXBVLXKXPKWGAUPUQURUSXCXHXJKZXCVLXBXQXCWIEFZVLXBXQAWGUBZXBXRVLWLEFXQWG
      UJWIAWLUTVAVBVCVDXCXEXLKZVLXBXBVLXTXCXBVLXTXCXRXBVLXTXSVLXRXBXAXTXFWIWGVM
      UTVAVBVCVEVDURVFQVGVHVIVJVK $.
      $( [22-Oct-2011] $) $( [21-Sep-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $( Multiplication of natural numbers is commutative.  Theorem 4K(5) of
       [Enderton] p. 81.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnmcom $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) = ( B .o A ) ) $=
      ( vx vy com wcel comu co wceq cv wi opreq1 opreq2 eqeq12d imbi2d c0 csuc
      nnm0r nnm0 eqtr4d wa coa nnmsucr nnmsuc ancoms syl5ibr ex finds2 vtoclga
      imp ) AEFBEFZABGHZBAGHZIZUKCJZBGHZBUOGHZIZKUKUNKCAEUOAIZURUNUKUSUPULUQUMU
      OABGLUOABGMNOURPBGHZBPGHZIDJZBGHZBVBGHZIZVBQZBGHZBVFGHZIZUKCDUOPIUPUTUQVA
      UOPBGLUOPBGMNUOVBIUPVCUQVDUOVBBGLUOVBBGMNUOVFIUPVGUQVHUOVFBGLUOVFBGMNUKUT
      PVABRBSTVBEFZUKVEVIKVEVIVJUKUAZVCBUBHZVDBUBHZIVCVDBUBLVKVGVLVHVMVBBUCUKVJ
      VHVMIBVBUDUENUFUGUHUIUJ $.
      $( [22-Oct-2011] $) $( [21-Sep-1995] $)
  $}

  $( Cancellation law for addition of natural numbers. $)
  nnacan $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                   ( ( A +o B ) = ( A +o C ) <-> B = C ) ) $=
    ( com wcel con0 coa co wceq wb nnon oacan syl3an ) ADEAFEBDEBFECDECFEABGHAC
    GHIBCIJAKBKCKABCLM $.
    $( [27-Oct-1995] $)

  $( Weak ordering property of addition. $)
  nnaword $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                ( A C_ B <-> ( C +o A ) C_ ( C +o B ) ) ) $=
    ( com wcel con0 wss coa co wb nnon oaword syl3an ) ADEAFEBDEBFECDECFEABGCAH
    ICBHIGJAKBKCKABCLM $.
    $( [17-Sep-1995] $)

  $( Weak ordering property of addition. $)
  nnaword1 $p |- ( ( A e. om /\ B e. om ) -> A C_ ( A +o B ) ) $=
    ( com wcel con0 coa co wss nnon oaword1 syl2an ) ACDAEDBEDAABFGHBCDAIBIABJK
    $.
    $( [9-Nov-2002] $)

  $( Weak ordering property of addition. $)
  nnaword2 $p |- ( ( A e. om /\ B e. om ) -> A C_ ( B +o A ) ) $=
    ( com wcel wa coa co nnaword1 nnacom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [9-Nov-2002] $)

  $( Ordering property of multiplication.  Half of Proposition 8.19 of
     [TakeutiZaring] p. 63, limited to natural numbers. $)
  nnmordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) -> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel c0 wa comu co wi con0 nnon omordi ex syl2an com23 imp3a 3adant1
    ) BDEZCDEZABEZFCEZGCAHICBHIEZJADESTGZUAUBUCUDUBUAUCSBKEZCKEZUBUAUCJZJTBLCLU
    EUFGUBUGABCMNOPQR $.
    $( [18-Sep-1995] $)

  $( Ordering property of multiplication.  Proposition 8.19 of [TakeutiZaring]
     p. 63, limited to natural numbers. $)
  nnmord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) <-> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel con0 c0 wa comu co wb nnon omord syl3an ) ADEAFEBDEBFECDECFEABEG
    CEHCAIJCBIJEKALBLCLABCMN $.
    $( [22-Jan-1996] $)

  $( Cancellation law for multiplication of natural numbers. $)
  nnmcan $p |- ( ( ( A e. om /\ B e. om /\ C e. om ) /\ (/) e. A ) ->
                   ( ( A .o B ) = ( A .o C ) <-> B = C ) ) $=
    ( com wcel con0 c0 comu co wceq wb nnon omcan syl3anl ) ADEAFEBDEBFECDECFEG
    AEABHIACHIJBCJKALBLCLABCMN $.
    $( [26-Oct-1995] $)

  ${
    $d x A $.  $d x B $.
    $( Equivalence for ordering.  Compare Exercise 23 of [Enderton] p. 88. $)
    nnaordex $p |- ( ( A e. om /\ B e. om ) -> ( A e. B <->
                  E. x e. om ( (/) e. x /\ ( A +o x ) = B ) ) ) $=
      ( com wcel con0 c0 cv coa co wceq wa wrex wb nnon oaordex sylan2 wi
      eleq1 bicomd nnarcl sylan9bbr simpr syl6bi exp31 com23 adantld com24
      imp4b a1i jcad anim1i impbid1 rexbidv2 bitrd sylan ) BDEZBFEZCDEZBCEZGAHZ
      EZBVAIJZCKZLZADMZNBOURUSLZUTVEAFMZVFUSURCFEUTVHNCOABCPQVGVEVEAFDVGVAFEZVE
      LZVADEZVELVGVJVKVEURUSVIVEVKURVEVIUSVKURVDVIUSVKRZRVBURVIVDVLURVIVDVLURVI
      LZVDLUSUQVKLZVKVDUSVCDEZVMVNVDVOUSVCCDSTBVAUAUBUQVKUCUDUEUFUGUHUIVJVERVGV
      IVEUCUJUKVKVIVEVAOULUMUNUOUP $.
      $( [5-Dec-1995] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for weak ordering of natural numbers. $)
    nnawordex $p |- ( ( A e. om /\ B e. om ) -> ( A C_ B <->
                  E. x e. om ( A +o x ) = B ) ) $=
      ( com wcel con0 wss cv coa co wceq wrex wb nnon wa oawordex sylan2 wi
      eleq1 bicomd nnarcl sylan9bbr simpr syl6bi exp31 com23 com24 imp4b a1i
      jcad anim1i impbid1 rexbidv2 bitrd sylan ) BDEZBFEZCDEZBCGZBAHZIJZCKZADLZ
      MBNUQUROZUSVBAFLZVCURUQCFEUSVEMCNABCPQVDVBVBAFDVDUTFEZVBOZUTDEZVBOVDVGVHV
      BUQURVFVBVHUQVBVFURVHUQVFVBURVHRZUQVFVBVIUQVFOZVBOURUPVHOZVHVBURVADEZVJVK
      VBVLURVACDSTBUTUAUBUPVHUCUDUEUFUGUHVGVBRVDVFVBUCUIUJVHVFVBUTNUKULUMUNUO
      $.
      $( [8-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Lemma for ~ oaabs . $)
    oaabslem $p |- ( ( om e. On /\ A e. om ) -> ( A +o om ) = om ) $=
      ( vx com con0 wcel wa coa co wss cv ciun wlim wceq nnon limom jctr oalim
      syl2an wral word ordom nnacl ordelss sylancr r19.21aiva iunss sylibr
      adantr eqsstrd ancoms oaword2 sylan2 eqssd ) CDEZACEZFACGHZCUOUNUPCIUOUNF
      UPBCABJZGHZKZCUOADEZUNCLZFUPUSMUNANZUNVAOPBACDQRUOUSCIZUNUOURCIZBCSVCUOVD
      BCUOUQCEFCTURCEVDUAAUQUBCURUCUDUEBCURCUFUGUHUIUJUOUNUTCUPIVBCAUKULUM $.
      $( [9-Dec-2004] $)

    $( Ordinal addition absorbs a natural number added to the left of a
       transfinite number.  Proposition 8.10 of [TakeutiZaring] p. 59. $)
    oaabs $p |- ( ( ( A e. om /\ B e. On ) /\ om C_ B ) -> ( A +o B ) = B ) $=
      ( vx vy com wcel con0 wss coa co wceq wi wa cvv ssexg ex word ordom
      elong mpbiri syl6com imp cv csuc opreq2 id eqeq12d imbi2d oaabslem nnon
      oasuc sylan suceq sylan9eq exp31 com12 ad2antrr a2d wlim wral bi2.04
      ralbii r19.21v bitri ciun cdif cun cuni limom jctr oalim syl2an ancoms
      limuni ax-mp syl6eq eqtr3d adantl impexp wn wb limord ordelon eloni
      ordtri1 mpan 3syl pm5.32da eldif syl6bbr imbi1d syl5bbr ralbidv2 iuneq2
      uniiun syl6eqr syl6bi adantlr adantr uneq12d visset jctl iunxun undif2
      ssequn1 biimpi syl5eq iuneq1 syl syl5reqr an32s ad2ant2r uniun unieqd
      3eqtr4d exp43 com23 syl5bi com4r impcom tfindsg mpdan com3r imp31 ) AEFZB
      GFZEBHZABIJZBKZYFYGYEYIYFYGYEYILZYFYGMEGFZYJYFYGYKYGYFENFZYKYGYFYLEBGOPYL
      YKEQZRENSTUAUBYFYKYGYJYEACUCZIJZYNKZLZYEAEIJZEKZLYEADUCZIJZYTKZLZYEAYTUDZ
      IJZUUDKZLYJCDBEYNEKZYPYSYEUUGYOYRYNEYNEAIUEUUGUFUGUHYNYTKZYPUUBYEUUHYOUUA
      YNYTYNYTAIUEUUHUFUGUHYNUUDKZYPUUFYEUUIYOUUEYNUUDYNUUDAIUEUUIUFUGUHYNBKZYP
      YIYEUUJYOYHYNBYNBAIUEUUJUFUGUHYKYEYSAUIZPYTGFZYKMEYTHZMYEUUBUUFUULYEUUBUU
      FLZLYKUUMYEUULUUNYEUULUUBUUFYEUULMUUBUUEUUAUDZUUDYEAGFZUULUUEUUOKAUJZAYTU
      KULUUAYTUMUNUOUPUQURYNUSZEYNHZYKUUMUUCLZDYNUTZYQLZYKUURUUSMZUVBUVCUVAYEYK
      YPUVAYEUUMUUBLZDYNUTZLZUVCYEYKYPLZLUVAYEUVDLZDYNUTUVFUUTUVHDYNUUMYEUUBVAV
      BYEUVDDYNVCVDUVCYEUVEUVGUVCUVEYEUVGUVCUVEYEYKYPUVCUVEMZYEYKMZMZDEUUAVEZDY
      NEVFZUUAVEZVGZEVHZUVMVHZVGZYOYNUVKUVLUVPUVNUVQUVJUVLUVPKUVIUVJYRUVLUVPYEU
      UPYKEUSZMYRUVLKYKUUQYKUVSVIVJDAEGVKVLUVJYREUVPYKYEYSUUKVMUVSEUVPKVIEVNVOV
      PVQVRUVIUVNUVQKZUVJUURUVEUVTUUSUURUVEUVTUURUVEUUBDUVMUTZUVTUURUVDUUBDYNUV
      MYTYNFZUVDLUWBUUMMZUUBLUURYTUVMFZUUBLUWBUUMUUBVSUURUWCUWDUUBUURUWCUWBYTEF
      VTZMUWDUURUWBUUMUWEUURUWBMUULYTQZUUMUWEWAZUURYNQUWBUULYNWBYNYTWCULYTWDYMU
      WFUWGREYTWEWFWGWHYTYNEWIWJWKWLWMUWAUVNDUVMYTVEUVQDUVMUUAYTWNDUVMWOWPWQUBW
      RWSWTUVCYEYOUVOKZUVEYKUURYEUUSUWHUURYEMUUSYODYNUUAVEZUVOYEUURYOUWIKZYEUUP
      YNNFZUURMUWJUURUUQUURUWKCXAXBDAYNNVKVLVMUUSUVODEUVMVGZUUAVEZUWIDEUVMUUAXC
      UUSUWLYNKUWMUWIKUUSUWLEYNVGZYNEYNXDUUSUWNYNKEYNXEXFXGZDUWLYNUUAXHXIXJUNXK
      XLUVCYNUVRKUVEUVJUURUUSYNYNVHZUVRYNVNUUSUVRUWLVHUWPEUVMXMUUSUWLYNUWOXNXJU
      NUQXOXPXQURXRXSXTXKYAXKYBPYCYD $.
      $( [9-Dec-2004] $)
  $}

  $( One is a natural number. $)
  1onn $p |- 1o e. om $=
    ( c1o c0 csuc com df-1o wcel peano1 peano2 ax-mp eqeltri ) ABCZDEBDFKDFGBHI
    J $.
    $( [29-Oct-1995] $)

  $( The ordinal 2 is a natural number. $)
  2onn $p |- 2o e. om $=
    ( c2o c1o csuc com df-2o wcel 1onn peano2 ax-mp eqeltri ) ABCZDEBDFKDFGBHIJ
    $.
    $( [28-Sep-2004] $)

  ${
    $d x y z A $.
    $( A natural number is even iff its successor is odd. $)
    nneob $p |- ( A e. om ->
      ( E. x e. om A = ( 2o .o x ) <-> -. E. x e. om suc A = ( 2o .o x ) ) ) $=
      ( vy vz com wcel c2o cv comu co wceq wrex csuc wn opreq2 eqeq2d cbvrexv
      wa con0 nnon id oneo syl3an 3com23 3expa nrexdv r19.23aiva sylbi wi c0
      suceq eqeq1d rexbidv notbid eqeq1 imbi12d peano1 eqid 2on om0 ax-mp
      syl6eq rcla4ev mp2an a1i c1o coa 1on oaass mp3an23 o1p1e2 opreq2i
      syl6req oa1suc opreq1d suceloni syl 3eqtrd eqcomd ad2antrr opreq1 2onn
      1onn nndi mp3an13 om1 sylan9eqr adantll eqtrd ex nnacl mpan2 adantl
      jctild syl6 r19.23adva syl5bi con3d con1 syl9 finds impbid2 ) BEFBGAHZIJZ
      KZAELZBMZXDKZAELZNZXFBGCHZIJZKZCELXJXEXMACEXCXKKZXDXLBXCXKGIOZPQXMXJCEXKE
      FZXMRXHAEXPXMXCEFZXHNZXPXQXMXRXPXKSFXQXCSFXMXMXRXKTXCTXMUAXKXCBUBUCUDUEUF
      UGUHXKMZXDKZAELZNZXKXDKZAELZUIUJMZXDKZAELZNZUJXDKZAELZUIDHZMZXDKZAELZNZYK
      XDKZAELZUIZYLMZXDKZAELZNZYNUIXJXFUICDBXKUJKZYBYHYDYJUUCYAYGUUCXTYFAEUUCXS
      YEXDXKUJUKULUMUNUUCYCYIAEXKUJXDUOUMUPXKYKKZYBYOYDYQUUDYAYNUUDXTYMAEUUDXSY
      LXDXKYKUKULUMUNUUDYCYPAEXKYKXDUOUMUPXKYLKZYBUUBYDYNUUEYAUUAUUEXTYTAEUUEXS
      YSXDXKYLUKULUMUNUUEYCYMAEXKYLXDUOUMUPXKBKZYBXJYDXFUUFYAXIUUFXTXHAEUUFXSXG
      XDXKBUKULUMUNUUFYCXEAEXKBXDUOUMUPYJYHUJEFUJUJKZYJUQUJURYIUUGAUJEXCUJKZXDU
      JUJUUHXDGUJIJZUJXCUJGIOGSFZUUIUJKUSGUTVAVBPVCVDVEYKEFZUUBYQNYRYNUUKYQUUAY
      QYKXLKZCELUUKUUAYPUULACEXNXDXLYKXOPQUUKUULUUACEUUKXPRZUULXKVFVGJZEFZYSGUU
      NIJZKZRUUAUUMUULUUQUUOUUMUULUUQUUMUULRYSYKGVGJZUUPUUKYSUURKXPUULUUKUURYSU
      UKYKSFZUURYSKYKTUUSUURYKVFVGJZVFVGJZYLVFVGJZYSUUSUVAYKVFVFVGJZVGJZUURUUSV
      FSFZUVEUVAUVDKVHVHYKVFVFVIVJUVCGYKVGVKVLVMUUSUUTYLVFVGYKVNVOUUSYLSFUVBYSK
      YKVPYLVNVQVRVQVSVTXPUULUURUUPKUUKUULXPUURXLGVGJZUUPYKXLGVGWAXPUUPXLGVFIJZ
      VGJZUVFGEFXPVFEFZUUPUVHKWBWCGXKVFWDWEUVGGXLVGUUJUVGGKUSGWFVAVLVMWGWHWIWJX
      PUUOUUKXPUVIUUOWCXKVFWKWLWMWNYTUUQAUUNEXCUUNKXDUUPYSXCUUNGIOPVCWOWPWQWRYN
      YQWSWTXAXB $.
      $( [26-Jan-2006] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v F $.
    $( Lemma for ~ omsmo . $)
    omsmolem $p |- ( y e. om -> ( ( ( A C_ On /\ F : om --> A ) /\
        A. x e. om ( F ` x ) e. ( F ` suc x ) ) ->
       ( z e. y -> ( F ` z ) e. ( F ` y ) ) ) ) $=
      ( vw cv wcel cfv wi c0 csuc con0 wss com wf wa wral wceq eleq2 fveq2
      eleq2d imbi12d noel pm2.21i a1i wo visset elsuc suceq fveq2d eleq12d
      rcla4cva adantll peano2b ffvelrn sylan2b ssel ontr1 exp3a com23 syl56
      imp31 adantlr mpd imim2d imp eleq1d syl5ibrcom adantr jaod syl5bi exp31
      com12 finds2 ) CGZBGZHZVPEIZVQEIZHZJVPKHZVSKEIZHZJZVPFGZHZVSWFEIZHZJZVPWF
      LZHZVSWKEIZHZJZDMNZODEPZQZAGZEIZWSLZEIZHZAORZQZBFVQKSZVRWBWAWDVQKVPTXFVTW
      CVSVQKEUAUBUCVQWFSZVRWGWAWIVQWFVPTXGVTWHVSVQWFEUAUBUCVQWKSZVRWLWAWNVQWKVP
      TXHVTWMVSVQWKEUAUBUCWEXEWBWDVPUDUEUFXEWFOHZWJWOJXEXIWJWOWLWGVPWFSZUGXEXIQ
      ZWJQZWNVPWFCUHUIXLWGWNXJXKWJWGWNJXKWIWNWGXKWHWMHZWIWNJZXDXIXMWRXCXMAWFOWS
      WFSZWTWHXBWMWSWFEUAXOXAWKEWSWFUJUKULUMZUNWRXIXMXNJZXDWPWQXIXQWPWQXIXQWQXI
      QWMDHZWPWMMHZXQXIWQWKOHXRWFUOODWKEUPUQDMWMURXSWIXMWNXSWIXMWNVSWHWMUSUTVAV
      BUTVCVDVEVFVGXKXJWNJZWJXDXIXTWRXDXIQWNXJXMXPXJVSWHWMVPWFEUAVHVIUNVJVKVLVM
      VNVO $.
      $( [30-Nov-2003] $)

    $( A strictly monotonic ordinal function on the set of natural numbers is
       one-to-one. $)
    omsmo $p |- ( ( ( A C_ On /\ F : om --> A ) /\
          A. x e. om ( F ` x ) e. ( F ` suc x ) ) -> F : om -1-1-> A ) $=
      ( vy vz con0 wss com wf wa cv cfv csuc wcel wral wceq wi wf1 simplr wo
      wn omsmolem adantl imp adantr orim12d ancoms con3d wb word ffvelrn ssel
      syl5 expdimp eloni syl6 anim12d ordtri3 syl adantlr nnord syl2an 3imtr4d
      exp32 r19.21adv r19.21aiv dff13 sylanbrc ) BFGZHBCIZJZAKZCLVLMCLNAHOZJZVJ
      DKZCLZEKZCLZPZVOVQPZQZEHOZDHOHBCRVIVJVMSVNWBDHVNVOHNZWAEHVNWCVQHNZWAVNWCW
      DJZJZVPVRNZVRVPNZTZUAZVOVQNZVQVONZTZUAZVSVTWFWMWIWEVNWMWIQWEVNJWKWGWLWHWE
      VNWKWGQZWDVNWOQWCAEDBCUBUCUDWEVNWLWHQZWCVNWPQWDADEBCUBUEUDUFUGUHVKWEVSWJU
      IZVMVKWEJVPUJZVRUJZJZWQVKWEWTVKWCWRWDWSVKWCVPFNZWRVIVJWCXAVJWCJVPBNVIXAHB
      VOCUKBFVPULUMUNVPUOUPVKWDVRFNZWSVIVJWDXBVJWDJVRBNVIXBHBVQCUKBFVRULUMUNVRU
      OUPUQUDVPVRURUSUTWEVTWNUIZVNWCVOUJVQUJXCWDVOVAVQVAVOVQURVBUCVCVDVEVFDEHBC
      VGVH $.
      $( [30-Nov-2003] $)
  $}


