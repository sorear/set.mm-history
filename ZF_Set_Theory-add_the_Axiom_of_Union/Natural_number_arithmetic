$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Natural number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Addition with zero.  Theorem 4I(A1) of [Enderton] p. 79. $)
  nna0 $p |- ( A e. om -> ( A +o (/) ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnon oa0 syl ) ABCADCAEFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Theorem 4J(A1) of [Enderton] p. 80. $)
  nnm0 $p |- ( A e. om -> ( A .o (/) ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnon om0 syl ) ABCADCAEFGEHAIAJK $.
    $( [20-Sep-1995] $)

  $( Addition with successor.  Theorem 4I(A2) of [Enderton] p. 79. $)
  nnasuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A +o suc B ) = suc ( A +o B ) ) $=
    ( com wcel con0 csuc coa co wceq nnon oasuc syl2an ) ACDAEDBEDABFGHABGHFIBC
    DAJBJABKL $.
    $( [20-Sep-1995] $)

  $( Multiplication with successor.  Theorem 4J(A2) of [Enderton] p. 80. $)
  nnmsuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A .o suc B ) = ( ( A .o B ) +o A ) ) $=
    ( com wcel con0 csuc comu co coa wceq nnon omsuc syl2an ) ACDAEDBEDABFGHABG
    HAIHJBCDAKBKABLM $.
    $( [20-Sep-1995] $)

  $( Addition to zero.  Remark in proof of Theorem 4K(2) of [Enderton]
     p. 81. $)
  nna0r $p |- ( A e. om -> ( (/) +o A ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnon oa0r syl ) ABCADCEAFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Exercise 16 of [Enderton] p. 82. $)
  nnm0r $p |- ( A e. om -> ( (/) .o A ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnon om0r syl ) ABCADCEAFGEHAIAJK $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Closure of addition of natural numbers.  Proposition 8.9 of
       [TakeutiZaring] p. 59.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnacl $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) e. om ) $=
      ( vx vy com wcel coa co cv wi wceq opreq2 eleq1d imbi2d c0 csuc nna0 ibir
      wa peano2 nnasuc syl5ibr expcom finds2 vtoclga impcom ) BEFAEFZABGHZEFZUG
      ACIZGHZEFZJUGUIJCBEUJBKZULUIUGUMUKUHEUJBAGLMNULAOGHZEFZADIZGHZEFZAUPPZGHZ
      EFZUGCDUJOKUKUNEUJOAGLMUJUPKUKUQEUJUPAGLMUJUSKUKUTEUJUSAGLMUGUOUGUNAEAQMR
      UGUPEFZURVAJURVAUGVBSZUQPZEFUQTVCUTVDEAUPUAMUBUCUDUEUF $.
      $( [22-Oct-2011] $) $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure of multiplication of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnmcl $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) e. om ) $=
      ( vx vy com wcel comu co cv wi wceq opreq2 eleq1d imbi2d csuc nnm0 peano1
      c0 syl6eqel expcom coa nnacl adantr nnmsuc sylibrd finds2 vtoclga impcom
      wa ) BEFAEFZABGHZEFZUJACIZGHZEFZJUJULJCBEUMBKZUOULUJUPUNUKEUMBAGLMNUOARGH
      ZEFADIZGHZEFZAUROZGHZEFZUJCDUMRKUNUQEUMRAGLMUMURKUNUSEUMURAGLMUMVAKUNVBEU
      MVAAGLMUJUQREAPQSUJUREFZUTVCJUJVDUIZUTUSAUAHZEFZVCUJUTVGJVDUTUJVGUSAUBTUC
      VEVBVFEAURUDMUETUFUGUH $.
      $( [22-Oct-2011] $) $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Closure of exponentiation of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnecl $p |- ( ( A e. om /\ B e. om ) -> ( A ^o B ) e. om ) $=
      ( vy vx com wcel coe co cv wi wceq opreq2 eleq1d imbi2d c0 csuc con0 nnon
      c1o expcom oe0 syl df-1o peano1 peano2 ax-mp eqeltri syl6eqel comu adantr
      wa nnmcl oesuc syl2an sylibrd finds2 vtoclga impcom ) BEFAEFZABGHZEFZUSAC
      IZGHZEFZJUSVAJCBEVBBKZVDVAUSVEVCUTEVBBAGLMNVDAOGHZEFADIZGHZEFZAVGPZGHZEFZ
      USCDVBOKVCVFEVBOAGLMVBVGKVCVHEVBVGAGLMVBVJKVCVKEVBVJAGLMUSVFSEUSAQFZVFSKA
      RZAUAUBSOPZEUCOEFVOEFUDOUEUFUGUHUSVGEFZVIVLJUSVPUKZVIVHAUIHZEFZVLUSVIVSJV
      PVIUSVSVHAULTUJVQVKVREUSVMVGQFVKVRKVPVNVGRAVGUMUNMUOTUPUQUR $.
      $( [22-Oct-2011] $) $( [24-Mar-2007] $)
  $}

  $( Reverse closure law for addition of natural numbers.  Exercise 1 of
     [TakeutiZaring] p. 62 and its converse. $)
  nnarcl $p |- ( ( A e. On /\ B e. On ) ->
               ( ( A +o B ) e. om <-> ( A e. om /\ B e. om ) ) ) $=
    ( con0 wcel wa coa co com wss oaword1 word eloni ordom ordtr2 sylancl exp3a
    wi adantr mpd oaword2 ancoms adantl jcad nnacl impbid1 ) ACDZBCDZEZABFGZHDZ
    AHDZBHDZEUHUJUKULUHAUIIZUJUKQZABJUFUMUNQUGUFUMUJUKUFAKHKZUMUJEUKQALMAUIHNOP
    RSUHBUIIZUJULQZUGUFUPBATUAUGUPUQQUFUGUPUJULUGBKUOUPUJEULQBLMBUIHNOPUBSUCABU
    DUE $.
    $( [12-Dec-2004] $)

  ${
    $d x A $.  $d x y z B $.
    $( Addition of natural numbers is commutative.  Theorem 4K(2) of [Enderton]
       p. 81. $)
    nnacom $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) = ( B +o A ) ) $=
      ( vx com wcel coa co wceq cv wi c0 csuc opreq1 opreq2 eqeq12d imbi2d nna0
      wa suceq syl vy vz nna0r eqtr4d peano2b sylbi con0 suceloni oasuc anim12i
      nnon syl2an 3syl syl5ibr expcom a2d finds imp nnasuc ) ADEBDEZABFGZBAFGZH
      ZUTCIZBFGZBVDFGZHZJUTKBFGZBKFGZHZJUTUAIZBFGZBVKFGZHZJUTVKLZBFGZBVOFGZHZJU
      TVCJCUAAVDKHZVGVJUTVSVEVHVFVIVDKBFMVDKBFNOPVDVKHZVGVNUTVTVEVLVFVMVDVKBFMV
      DVKBFNOPVDVOHZVGVRUTWAVEVPVFVQVDVOBFMVDVOBFNOPVDAHZVGVCUTWBVEVAVFVBVDABFM
      VDABFNOPUTVHBVIBUCBQUDVKDEZUTVNVRUTWCVNVRJVNVRUTWCRZVLLZVMLZHVLVMSWDVPWEV
      QWFUTWCVPWEHZWCVOVDFGZVKVDFGZLZHZJWCVOKFGZVKKFGZLZHZJWCVOUBIZFGZVKWPFGZLZ
      HZJWCVOWPLZFGZVKXAFGZLZHZJWCWGJCUBBVSWKWOWCVSWHWLWJWNVDKVOFNVSWIWMHWJWNHV
      DKVKFNWIWMSTOPVDWPHZWKWTWCXFWHWQWJWSVDWPVOFNXFWIWRHWJWSHVDWPVKFNWIWRSTOPV
      DXAHZWKXEWCXGWHXBWJXDVDXAVOFNXGWIXCHWJXDHVDXAVKFNWIXCSTOPVDBHZWKWGWCXHWHV
      PWJWEVDBVOFNXHWIVLHWJWEHVDBVKFNWIVLSTOPWCWLVOWNWCVODEWLVOHVKUEVOQUFWCWMVK
      HWNVOHVKQWMVKSTUDWPDEZWCWTXEWCXIWTXEJWTXEWCXIRZWQLZWSLZHWQWSSXJXBXKXDXLWC
      VOUGEZWPUGEZXBXKHXIWCVKUGEZXMVKUKZVKUHTWPUKZVOWPUIULXJXOXNRXCWSHXDXLHWCXO
      XIXNXPXQUJVKWPUIXCWSSUMOUNUOUPUQURBVKUSOUNUOUPUQUR $.
      $( [6-May-1995] $)
  $}

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers. $)
  nnaordi $p |- ( ( B e. om /\ C e. om ) ->
               ( A e. B -> ( C +o A ) e. ( C +o B ) ) ) $=
    ( com wcel con0 coa co wi nnon oaordi syl2an ) BDEBFECFEABECAGHCBGHEICDEBJC
    JABCKL $.
    $( [3-Feb-1996] $)

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers, and its converse. $)
  nnaord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( C +o A ) e. ( C +o B ) ) ) $=
    ( com wcel con0 coa co wb nnon oaord syl3an ) ADEAFEBDEBFECDECFEABECAGHCBGH
    EIAJBJCJABCKL $.
    $( [7-Mar-1996] $)

  $( Ordering property of addition of natural numbers. $)
  nnaordr $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( A +o C ) e. ( B +o C ) ) ) $=
    ( com wcel w3a coa nnaord wceq nnacom ancoms 3adant2 3adant1 eleq12d bitrd
    co ) ADEZBDEZCDEZFZABECAGPZCBGPZEACGPZBCGPZEABCHTUAUCUBUDQSUAUCIZRSQUECAJKL
    RSUBUDIZQSRUFCBJKMNO $.
    $( [9-Nov-2002] $)

  $( Addition of natural numbers is associative.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/3
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case..)  Theorem 4K(1) of [Enderton] p. 81. $)
  nnaass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A +o B ) +o C ) = ( A +o ( B +o C ) ) ) $=
    ( com wcel con0 coa co wceq nnon oaass syl3an ) ADEAFEBDEBFECDECFEABGHCGHAB
    CGHGHIAJBJCJABCKL $.
    $( [20-Sep-1995] $)

  $( Distributive law for natural numbers.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/4
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case.)  Theorem 4K(3) of [Enderton] p. 81. $)
  nndi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( A .o ( B +o C ) ) = ( ( A .o B ) +o ( A .o C ) ) ) $=
    ( com wcel con0 coa co comu wceq nnon odi syl3an ) ADEAFEBDEBFECDECFEABCGHI
    HABIHACIHGHJAKBKCKABCLM $.
    $( [20-Sep-1995] $)

  $( Multiplication of natural numbers is associative.  (For brevity, this is
     just a special case of the proof for ordinals.  A direct proof would be
     about 1/3 the size of the ordinal proof, since it would use finite
     induction and not require the limit ordinal case..)  Theorem 4K(4) of
     [Enderton] p. 81. $)
  nnmass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A .o B ) .o C ) = ( A .o ( B .o C ) ) ) $=
    ( com wcel con0 comu co wceq nnon omass syl3an ) ADEAFEBDEBFECDECFEABGHCGHA
    BCGHGHIAJBJCJABCKL $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Multiplication with successor.  Exercise 16 of [Enderton] p. 82.  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    nnmsucr $p |- ( ( A e. om /\ B e. om ) ->
                  ( suc A .o B ) = ( ( A .o B ) +o B ) ) $=
      ( vx vy com wcel csuc comu co coa wceq cv wi opreq2 opreq12d eqeq12d nnm0
      id c0 syl imbi2d peano1 nnmcl mpan2 nna0 peano2 3eqtr4rd wa opreq1 nnmsuc
      peano2b sylanb opreq1d nnacom suceq nnasuc ancoms 3eqtr4d nnaass syl3an3b
      opreq2d syl3an1 3expb anidms an42s syl5ibr expcom finds2 vtoclga impcom
      eqtrd ) BEFAEFZAGZBHIZABHIZBJIZKZVLVMCLZHIZAVRHIZVRJIZKZMVLVQMCBEVRBKZWBV
      QVLWCVSVNWAVPVRBVMHNWCVTVOVRBJVRBAHNWCROPUAWBVMSHIZASHIZSJIZKVMDLZHIZAWGH
      IZWGJIZKZVMWGGZHIZAWLHIZWLJIZKZVLCDVRSKZVSWDWAWFVRSVMHNWQVTWEVRSJVRSAHNWQ
      ROPVRWGKZVSWHWAWJVRWGVMHNWRVTWIVRWGJVRWGAHNWRROPVRWLKZVSWMWAWOVRWLVMHNWSV
      TWNVRWLJVRWLAHNWSROPVLWESWFWDAQVLWEEFZWFWEKVLSEFWTUBASUCUDWEUETVLVMEFZWDS
      KAUFVMQTUGVLWGEFZWKWPMWKWPVLXBUHZWHVMJIZWJVMJIZKWHWJVMJUIXCWMXDWOXEVLXAXB
      WMXDKAUKZVMWGUJULXCWOWIAJIZWLJIZXEXCWNXGWLJAWGUJUMXCWIAWLJIZJIZWIWGVMJIZJ
      IZXHXEXCXIXKWIJXCAWGJIZGZWGAJIZGZXIXKXCXMXOKXNXPKAWGUNXMXOUOTAWGUPXBVLXKX
      PKWGAUPUQURVAXCXHXJKZXCVLXBXQXCWIEFZVLXBXQAWGUCZXBXRVLWLEFXQWGUKWIAWLUSUT
      VBVCVDXCXEXLKZVLXBXBVLXTXCXBVLXTXCXRXBVLXTXSVLXRXBXAXTXFWIWGVMUSUTVBVCVEV
      DURVKPVFVGVHVIVJ $.
      $( [22-Oct-2011] $) $( [21-Sep-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $( Multiplication of natural numbers is commutative.  Theorem 4K(5) of
       [Enderton] p. 81.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    nnmcom $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) = ( B .o A ) ) $=
      ( vx vy com wcel comu co wceq cv wi opreq1 opreq2 eqeq12d imbi2d c0 nnm0r
      csuc nnm0 coa eqtr4d wa nnmsucr nnmsuc ancoms syl5ibr finds2 vtoclga imp
      ex ) AEFBEFZABGHZBAGHZIZUKCJZBGHZBUOGHZIZKUKUNKCAEUOAIZURUNUKUSUPULUQUMUO
      ABGLUOABGMNOURPBGHZBPGHZIDJZBGHZBVBGHZIZVBRZBGHZBVFGHZIZUKCDUOPIUPUTUQVAU
      OPBGLUOPBGMNUOVBIUPVCUQVDUOVBBGLUOVBBGMNUOVFIUPVGUQVHUOVFBGLUOVFBGMNUKUTP
      VABQBSUAVBEFZUKVEVIKVEVIVJUKUBZVCBTHZVDBTHZIVCVDBTLVKVGVLVHVMVBBUCUKVJVHV
      MIBVBUDUENUFUJUGUHUI $.
      $( [22-Oct-2011] $) $( [21-Sep-1995] $)
  $}

  $( Cancellation law for addition of natural numbers. $)
  nnacan $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                   ( ( A +o B ) = ( A +o C ) <-> B = C ) ) $=
    ( com wcel con0 coa co wceq wb nnon oacan syl3an ) ADEAFEBDEBFECDECFEABGHAC
    GHIBCIJAKBKCKABCLM $.
    $( [27-Oct-1995] $)

  $( Weak ordering property of addition. $)
  nnaword $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                ( A C_ B <-> ( C +o A ) C_ ( C +o B ) ) ) $=
    ( com wcel con0 wss coa co wb nnon oaword syl3an ) ADEAFEBDEBFECDECFEABGCAH
    ICBHIGJAKBKCKABCLM $.
    $( [17-Sep-1995] $)

  $( Weak ordering property of addition. $)
  nnaword1 $p |- ( ( A e. om /\ B e. om ) -> A C_ ( A +o B ) ) $=
    ( com wcel con0 coa co wss nnon oaword1 syl2an ) ACDAEDBEDAABFGHBCDAIBIABJK
    $.
    $( [9-Nov-2002] $)

  $( Weak ordering property of addition. $)
  nnaword2 $p |- ( ( A e. om /\ B e. om ) -> A C_ ( B +o A ) ) $=
    ( com wcel wa coa co nnaword1 nnacom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [9-Nov-2002] $)

  $( Ordering property of multiplication.  Half of Proposition 8.19 of
     [TakeutiZaring] p. 63, limited to natural numbers. $)
  nnmordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) -> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel c0 wa comu co wi con0 nnon omordi ex syl2an com23 imp3a 3adant1
    ) BDEZCDEZABEZFCEZGCAHICBHIEZJADESTGZUAUBUCUDUBUAUCSBKEZCKEZUBUAUCJZJTBLCLU
    EUFGUBUGABCMNOPQR $.
    $( [18-Sep-1995] $)

  $( Ordering property of multiplication.  Proposition 8.19 of [TakeutiZaring]
     p. 63, limited to natural numbers. $)
  nnmord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) <-> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel con0 c0 wa comu co wb nnon omord syl3an ) ADEAFEBDEBFECDECFEABEG
    CEHCAIJCBIJEKALBLCLABCMN $.
    $( [22-Jan-1996] $)

  $( Cancellation law for multiplication of natural numbers. $)
  nnmcan $p |- ( ( ( A e. om /\ B e. om /\ C e. om ) /\ (/) e. A ) ->
                   ( ( A .o B ) = ( A .o C ) <-> B = C ) ) $=
    ( com wcel con0 c0 comu co wceq wb nnon omcan syl3anl ) ADEAFEBDEBFECDECFEG
    AEABHIACHIJBCJKALBLCLABCMN $.
    $( [26-Oct-1995] $)

  ${
    $d x A $.  $d x B $.
    $( Equivalence for ordering.  Compare Exercise 23 of [Enderton] p. 88. $)
    nnaordex $p |- ( ( A e. om /\ B e. om ) -> ( A e. B <->
                  E. x e. om ( (/) e. x /\ ( A +o x ) = B ) ) ) $=
      ( com wcel con0 c0 cv coa co wceq wa wrex wb nnon oaordex sylan2 wi eleq1
      simpr bicomd nnarcl sylan9bbr syl6bi exp31 com23 adantld com24 imp4b jcad
      a1i anim1i impbid1 rexbidv2 bitrd sylan ) BDEZBFEZCDEZBCEZGAHZEZBVAIJZCKZ
      LZADMZNBOURUSLZUTVEAFMZVFUSURCFEUTVHNCOABCPQVGVEVEAFDVGVAFEZVELZVADEZVELV
      GVJVKVEURUSVIVEVKURVEVIUSVKURVDVIUSVKRZRVBURVIVDVLURVIVDVLURVILZVDLUSUQVK
      LZVKVDUSVCDEZVMVNVDVOUSVCCDSUABVAUBUCUQVKTUDUEUFUGUHUIVJVERVGVIVETUKUJVKV
      IVEVAOULUMUNUOUP $.
      $( [5-Dec-1995] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for weak ordering of natural numbers. $)
    nnawordex $p |- ( ( A e. om /\ B e. om ) -> ( A C_ B <->
                  E. x e. om ( A +o x ) = B ) ) $=
      ( com wcel con0 wss cv coa co wceq wrex wb wa oawordex sylan2 eleq1 simpr
      nnon wi bicomd nnarcl sylan9bbr syl6bi exp31 com23 com24 imp4b a1i anim1i
      jcad impbid1 rexbidv2 bitrd sylan ) BDEZBFEZCDEZBCGZBAHZIJZCKZADLZMBSUQUR
      NZUSVBAFLZVCURUQCFEUSVEMCSABCOPVDVBVBAFDVDUTFEZVBNZUTDEZVBNVDVGVHVBUQURVF
      VBVHUQVBVFURVHUQVFVBURVHTZUQVFVBVIUQVFNZVBNURUPVHNZVHVBURVADEZVJVKVBVLURV
      ACDQUABUTUBUCUPVHRUDUEUFUGUHVGVBTVDVFVBRUIUKVHVFVBUTSUJULUMUNUO $.
      $( [8-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Lemma for ~ oaabs . $)
    oaabslem $p |- ( ( om e. On /\ A e. om ) -> ( A +o om ) = om ) $=
      ( vx com con0 wcel wa coa co wss cv ciun wlim wceq nnon limom jctr syl2an
      oalim wral word ordom nnacl ordelss sylancr ralrimiva iunss sylibr adantr
      eqsstrd ancoms oaword2 sylan2 eqssd ) CDEZACEZFACGHZCUOUNUPCIUOUNFUPBCABJ
      ZGHZKZCUOADEZUNCLZFUPUSMUNANZUNVAOPBACDRQUOUSCIZUNUOURCIZBCSVCUOVDBCUOUQC
      EFCTURCEVDUAAUQUBCURUCUDUEBCURCUFUGUHUIUJUOUNUTCUPIVBCAUKULUM $.
      $( [9-Dec-2004] $)

    $( Ordinal addition absorbs a natural number added to the left of a
       transfinite number.  Proposition 8.10 of [TakeutiZaring] p. 59. $)
    oaabs $p |- ( ( ( A e. om /\ B e. On ) /\ om C_ B ) -> ( A +o B ) = B ) $=
      ( vy com wcel con0 coa co wceq wi cvv opreq2 eqeq12d imbi2d wral ciun cun
      wa id cuni vx wss ssexg word ordom elong mpbiri syl6com imp csuc oaabslem
      ex nnon oasuc sylan suceq sylan9eq exp31 com12 ad2antrr a2d bi2.04 ralbii
      cv wlim r19.21v bitri cdif limom oalim syl2an ancoms limuni syl6eq eqtr3d
      jctr ax-mp adantl impexp wn wb limord ordelon eloni ordtri1 mpan pm5.32da
      3syl syl6bbr imbi1d syl5bbr ralbidv2 iuneq2 uniiun syl6eqr syl6bi adantlr
      eldif adantr uneq12d vex jctl iunxun undif2 biimpi syl5eq iuneq1 syl5reqr
      ssequn1 syl an32s ad2ant2r uniun unieqd 3eqtr4d exp43 com23 syl5bi impcom
      com4r tfindsg mpdan com3r imp31 ) ADEZBFEZDBUBZABGHZBIZYFYGYEYIYFYGYEYIJZ
      YFYGRDFEZYJYFYGYKYGYFDKEZYKYGYFYLDBFUCULYLYKDUDZUEDKUFUGUHUIYFYKYGYJYEAUA
      VDZGHZYNIZJZYEADGHZDIZJYEACVDZGHZYTIZJZYEAYTUJZGHZUUDIZJYJUACBDYNDIZYPYSY
      EUUGYOYRYNDYNDAGLUUGSMNYNYTIZYPUUBYEUUHYOUUAYNYTYNYTAGLUUHSMNYNUUDIZYPUUF
      YEUUIYOUUEYNUUDYNUUDAGLUUISMNYNBIZYPYIYEUUJYOYHYNBYNBAGLUUJSMNYKYEYSAUKZU
      LYTFEZYKRDYTUBZRYEUUBUUFUULYEUUBUUFJZJYKUUMYEUULUUNYEUULUUBUUFYEUULRUUBUU
      EUUAUJZUUDYEAFEZUULUUEUUOIAUMZAYTUNUOUUAYTUPUQURUSUTVAYNVEZDYNUBZYKUUMUUC
      JZCYNOZYQJZYKUURUUSRZUVBUVCUVAYEYKYPUVAYEUUMUUBJZCYNOZJZUVCYEYKYPJZJUVAYE
      UVDJZCYNOUVFUUTUVHCYNUUMYEUUBVBVCYEUVDCYNVFVGUVCYEUVEUVGUVCUVEYEUVGUVCUVE
      YEYKYPUVCUVERZYEYKRZRZCDUUAPZCYNDVHZUUAPZQZDTZUVMTZQZYOYNUVKUVLUVPUVNUVQU
      VJUVLUVPIUVIUVJYRUVLUVPYEUUPYKDVEZRYRUVLIYKUUQYKUVSVIVPCADFVJVKUVJYRDUVPY
      KYEYSUUKVLUVSDUVPIVIDVMVQVNVOVRUVIUVNUVQIZUVJUURUVEUVTUUSUURUVEUVTUURUVEU
      UBCUVMOZUVTUURUVDUUBCYNUVMYTYNEZUVDJUWBUUMRZUUBJUURYTUVMEZUUBJUWBUUMUUBVS
      UURUWCUWDUUBUURUWCUWBYTDEVTZRUWDUURUWBUUMUWEUURUWBRUULYTUDZUUMUWEWAZUURYN
      UDUWBUULYNWBYNYTWCUOYTWDYMUWFUWGUEDYTWEWFWHWGYTYNDWRWIWJWKWLUWAUVNCUVMYTP
      UVQCUVMUUAYTWMCUVMWNWOWPUIWQWSWTUVCYEYOUVOIZUVEYKUURYEUUSUWHUURYERUUSYOCY
      NUUAPZUVOYEUURYOUWIIZYEUUPYNKEZUURRUWJUURUUQUURUWKUAXAXBCAYNKVJVKVLUUSUVO
      CDUVMQZUUAPZUWICDUVMUUAXCUUSUWLYNIUWMUWIIUUSUWLDYNQZYNDYNXDUUSUWNYNIDYNXI
      XEXFZCUWLYNUUAXGXJXHUQXKXLUVCYNUVRIUVEUVJUURUUSYNYNTZUVRYNVMUUSUVRUWLTUWP
      DUVMXMUUSUWLYNUWOXNXHUQUTXOXPXQVAXRXTXSXKYAXKYBULYCYD $.
      $( [9-Dec-2004] $)
  $}

  $( One is a natural number. $)
  1onn $p |- 1o e. om $=
    ( c1o c0 csuc com df-1o wcel peano1 peano2 ax-mp eqeltri ) ABCZDEBDFKDFGBHI
    J $.
    $( [29-Oct-1995] $)

  $( The ordinal 2 is a natural number. $)
  2onn $p |- 2o e. om $=
    ( c2o c1o csuc com df-2o wcel 1onn peano2 ax-mp eqeltri ) ABCZDEBDFKDFGBHIJ
    $.
    $( [28-Sep-2004] $)

  ${
    $d x y z A $.
    $( A natural number is even iff its successor is odd. $)
    nneob $p |- ( A e. om ->
      ( E. x e. om A = ( 2o .o x ) <-> -. E. x e. om suc A = ( 2o .o x ) ) ) $=
      ( vy com wcel c2o comu co wceq wrex csuc wn eqeq2d wa con0 wi rexbidv c1o
      c0 coa vz cv opreq2 cbvrexv nnon id oneo syl3an 3com23 3expa nrexdv sylbi
      rexlimiva suceq eqeq1d notbid eqeq1 imbi12d peano1 eqid 2on ax-mp rcla4ev
      om0 syl6eq mp2an a1i oaass mp3an23 o1p1e2 opreq2i syl6req oa1suc suceloni
      1on opreq1d syl 3eqtrd eqcomd ad2antrr opreq1 2onn 1onn mp3an13 sylan9eqr
      nndi om1 adantll eqtrd ex nnacl mpan2 adantl jctild syl6 rexlimdva syl5bi
      con3d con1 syl9 finds impbid2 ) BDEBFAUBZGHZIZADJZBKZXDIZADJZLZXFBFCUBZGH
      ZIZCDJXJXEXMACDXCXKIZXDXLBXCXKFGUCZMUDXMXJCDXKDEZXMNXHADXPXMXCDEZXHLZXPXQ
      XMXRXPXKOEXQXCOEXMXMXRXKUEXCUEXMUFXKXCBUGUHUIUJUKUMULXKKZXDIZADJZLZXKXDIZ
      ADJZPSKZXDIZADJZLZSXDIZADJZPUAUBZKZXDIZADJZLZYKXDIZADJZPZYLKZXDIZADJZLZYN
      PXJXFPCUABXKSIZYBYHYDYJUUCYAYGUUCXTYFADUUCXSYEXDXKSUNUOQUPUUCYCYIADXKSXDU
      QQURXKYKIZYBYOYDYQUUDYAYNUUDXTYMADUUDXSYLXDXKYKUNUOQUPUUDYCYPADXKYKXDUQQU
      RXKYLIZYBUUBYDYNUUEYAUUAUUEXTYTADUUEXSYSXDXKYLUNUOQUPUUEYCYMADXKYLXDUQQUR
      XKBIZYBXJYDXFUUFYAXIUUFXTXHADUUFXSXGXDXKBUNUOQUPUUFYCXEADXKBXDUQQURYJYHSD
      ESSIZYJUSSUTYIUUGASDXCSIZXDSSUUHXDFSGHZSXCSFGUCFOEZUUISIVAFVDVBVEMVCVFVGY
      KDEZUUBYQLYRYNUUKYQUUAYQYKXLIZCDJUUKUUAYPUULACDXNXDXLYKXOMUDUUKUULUUACDUU
      KXPNZUULXKRTHZDEZYSFUUNGHZIZNUUAUUMUULUUQUUOUUMUULUUQUUMUULNYSYKFTHZUUPUU
      KYSUURIXPUULUUKUURYSUUKYKOEZUURYSIYKUEUUSUURYKRTHZRTHZYLRTHZYSUUSUVAYKRRT
      HZTHZUURUUSROEZUVEUVAUVDIVOVOYKRRVHVIUVCFYKTVJVKVLUUSUUTYLRTYKVMVPUUSYLOE
      UVBYSIYKVNYLVMVQVRVQVSVTXPUULUURUUPIUUKUULXPUURXLFTHZUUPYKXLFTWAXPUUPXLFR
      GHZTHZUVFFDEXPRDEZUUPUVHIWBWCFXKRWFWDUVGFXLTUUJUVGFIVAFWGVBVKVLWEWHWIWJXP
      UUOUUKXPUVIUUOWCXKRWKWLWMWNYTUUQAUUNDXCUUNIXDUUPYSXCUUNFGUCMVCWOWPWQWRYNY
      QWSWTXAXB $.
      $( [26-Jan-2006] $)
  $}

  ${
    $d y z w A $.  $d x y z w F $.
    $( Lemma for ~ omsmo .  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 1-Jan-2014.) $)
    omsmolem $p |- ( y e. om -> ( ( ( A C_ On /\ F : om --> A ) /\
        A. x e. om ( F ` x ) e. ( F ` suc x ) ) ->
       ( z e. y -> ( F ` z ) e. ( F ` y ) ) ) ) $=
      ( vw cv wcel cfv wi c0 csuc con0 com wa wceq eleq2 fveq2 eleq2d imbi12d
      wss wf wral noel pm2.21i a1i wo vex elsuc fveq2d eleq12d rcla4cva adantll
      suceq peano2b ffvelrn sylan2b ssel ontr1 exp3a com23 syl56 adantlr imim2d
      imp31 mpd imp eleq1d syl5ibrcom adantr jaod syl5bi exp31 com12 finds2 ) C
      GZBGZHZVPEIZVQEIZHZJVPKHZVSKEIZHZJZVPFGZHZVSWFEIZHZJZVPWFLZHZVSWKEIZHZJZD
      MUAZNDEUBZOZAGZEIZWSLZEIZHZANUCZOZBFVQKPZVRWBWAWDVQKVPQXFVTWCVSVQKERSTVQW
      FPZVRWGWAWIVQWFVPQXGVTWHVSVQWFERSTVQWKPZVRWLWAWNVQWKVPQXHVTWMVSVQWKERSTWE
      XEWBWDVPUDUEUFXEWFNHZWJWOJXEXIWJWOWLWGVPWFPZUGXEXIOZWJOZWNVPWFCUHUIXLWGWN
      XJXKWJWGWNJXKWIWNWGXKWHWMHZWIWNJZXDXIXMWRXCXMAWFNWSWFPZWTWHXBWMWSWFERXOXA
      WKEWSWFUNUJUKULZUMWRXIXMXNJZXDWPWQXIXQWPWQXIXQWQXIOWMDHZWPWMMHZXQXIWQWKNH
      XRWFUONDWKEUPUQDMWMURXSWIXMWNXSWIXMWNVSWHWMUSUTVAVBUTVEVCVFVDVGXKXJWNJZWJ
      XDXIXTWRXDXIOWNXJXMXPXJVSWHWMVPWFERVHVIUMVJVKVLVMVNVO $.
      $( [1-Jan-2014] $) $( [30-Nov-2003] $)

    $( A strictly monotonic ordinal function on the set of natural numbers is
       one-to-one.  (Unnecessary distinct variable restrictions were removed by
       David Abernethy, 1-Jan-2014.) $)
    omsmo $p |- ( ( ( A C_ On /\ F : om --> A ) /\
          A. x e. om ( F ` x ) e. ( F ` suc x ) ) -> F : om -1-1-> A ) $=
      ( vy vz con0 com wa cv cfv wcel wral wceq wi wo omsmolem adantl imp word
      wn wss wf csuc simplr adantr orim12d ancoms con3d wb ffvelrn ssel expdimp
      wf1 syl5 eloni syl6 anim12d ordtri3 adantlr nnord syl2an 3imtr4d ralrimdv
      syl exp32 ralrimiv dff13 sylanbrc ) BFUAZGBCUBZHZAIZCJVLUCCJKAGLZHZVJDIZC
      JZEIZCJZMZVOVQMZNZEGLZDGLGBCUMVIVJVMUDVNWBDGVNVOGKZWAEGVNWCVQGKZWAVNWCWDH
      ZHZVPVRKZVRVPKZOZTZVOVQKZVQVOKZOZTZVSVTWFWMWIWEVNWMWINWEVNHWKWGWLWHWEVNWK
      WGNZWDVNWONWCAEDBCPQRWEVNWLWHNZWCVNWPNWDADEBCPUERUFUGUHVKWEVSWJUIZVMVKWEH
      VPSZVRSZHZWQVKWEWTVKWCWRWDWSVKWCVPFKZWRVIVJWCXAVJWCHVPBKVIXAGBVOCUJBFVPUK
      UNULVPUOUPVKWDVRFKZWSVIVJWDXBVJWDHVRBKVIXBGBVQCUJBFVRUKUNULVRUOUPUQRVPVRU
      RVDUSWEVTWNUIZVNWCVOSVQSXCWDVOUTVQUTVOVQURVAQVBVEVCVFDEGBCVGVH $.
      $( [1-Jan-2014] $) $( [30-Nov-2003] $)
  $}


