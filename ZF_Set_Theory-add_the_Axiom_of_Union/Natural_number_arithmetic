$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Natural number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Addition with zero.  Theorem 4I(A1) of [Enderton] p. 79. $)
  nna0 $p |- ( A e. om -> ( A +o (/) ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnont oa0 syl ) ABCADCAEFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Theorem 4J(A1) of [Enderton] p. 80. $)
  nnm0 $p |- ( A e. om -> ( A .o (/) ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnont om0 syl ) ABCADCAEFGEHAIAJK $.
    $( [20-Sep-1995] $)

  $( Addition with successor.  Theorem 4I(A2) of [Enderton] p. 79. $)
  nnasuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A +o suc B ) = suc ( A +o B ) ) $=
    ( con0 wcel csuc coa co wceq com oasuc nnont syl2an ) ACDBCDABEFGABFGEHAIDB
    IDABJAKBKL $.
    $( [20-Sep-1995] $)

  $( Multiplication with successor.  Theorem 4J(A2) of [Enderton] p. 80. $)
  nnmsuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A .o suc B ) = ( ( A .o B ) +o A ) ) $=
    ( con0 wcel csuc comu co coa wceq com omsuc nnont syl2an ) ACDBCDABEFGABFGA
    HGIAJDBJDABKALBLM $.
    $( [20-Sep-1995] $)

  $( Addition to zero.  Remark in proof of Theorem 4K(2) of [Enderton]
     p. 81. $)
  nna0r $p |- ( A e. om -> ( (/) +o A ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnont oa0r syl ) ABCADCEAFGAHAIAJK $.
    $( [20-Sep-1995] $)

  $( Multiplication with zero.  Exercise 16 of [Enderton] p. 82. $)
  nnm0r $p |- ( A e. om -> ( (/) .o A ) = (/) ) $=
    ( com wcel con0 c0 comu co wceq nnont om0r syl ) ABCADCEAFGEHAIAJK $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Closure of addition of natural numbers.  Proposition 8.9 of
       [TakeutiZaring] p. 59. $)
    nnacl $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) e. om ) $=
      ( vx vy com wcel coa co cv wi c0 csuc wceq opreq2 eleq1d imbi2d nna0
      ibir wa nnasuc peano2 syl5bir expcom a2d finds impcom ) BEFAEFZABGHZEFZUG
      ACIZGHZEFZJUGAKGHZEFZJUGADIZGHZEFZJUGAUOLZGHZEFZJUGUIJCDBUJKMZULUNUGVAUKU
      MEUJKAGNOPUJUOMZULUQUGVBUKUPEUJUOAGNOPUJURMZULUTUGVCUKUSEUJURAGNOPUJBMZUL
      UIUGVDUKUHEUJBAGNOPUGUNUGUMAEAQORUOEFZUGUQUTUGVEUQUTJUGVESZUTUPLZEFUQVFUS
      VGEAUOTOUPUAUBUCUDUEUF $.
      $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure of multiplication of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63. $)
    nnmcl $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) e. om ) $=
      ( vx vy com wcel comu co cv wi c0 csuc wceq opreq2 eleq1d imbi2d nnm0
      peano1 syl6eqel wa coa nnmsuc nnacl syl5bir exp4b com24 pm2.43i com3r
      a2d finds impcom ) BEFAEFZABGHZEFZULACIZGHZEFZJULAKGHZEFZJULADIZGHZEFZJUL
      AUTLZGHZEFZJULUNJCDBUOKMZUQUSULVFUPUREUOKAGNOPUOUTMZUQVBULVGUPVAEUOUTAGNO
      PUOVCMZUQVEULVHUPVDEUOVCAGNOPUOBMZUQUNULVIUPUMEUOBAGNOPULURKEAQRSUTEFZULV
      BVEULVBVJVEULVBVJVEJJULVJVBULVEULVJVBULVEULVJTZVEVAAUAHZEFVBULTVKVDVLEAUT
      UBOVAAUCUDUEUFUGUHUIUJUK $.
      $( [20-Sep-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure of exponentiation of natural numbers.  Proposition 8.17 of
       [TakeutiZaring] p. 63. $)
    nnecl $p |- ( ( A e. om /\ B e. om ) -> ( A ^o B ) e. om ) $=
      ( vx vy com wcel coe co cv wi c0 csuc wceq opreq2 eleq1d imbi2d c1o con0
      nnont oe0 syl df-1o peano1 peano2 ax-mp eqeltr syl6eqel wa comu oesuc
      syl2an nnmcl syl5bir exp4b com24 pm2.43i com3r a2d finds impcom ) BEFAEFZ
      ABGHZEFZVAACIZGHZEFZJVAAKGHZEFZJVAADIZGHZEFZJVAAVILZGHZEFZJVAVCJCDBVDKMZV
      FVHVAVOVEVGEVDKAGNOPVDVIMZVFVKVAVPVEVJEVDVIAGNOPVDVLMZVFVNVAVQVEVMEVDVLAG
      NOPVDBMZVFVCVAVRVEVBEVDBAGNOPVAVGQEVAARFZVGQMASZATUAQKLZEUBKEFWAEFUCKUDUE
      UFUGVIEFZVAVKVNVAVKWBVNVAVKWBVNJJVAWBVKVAVNVAWBVKVAVNVAWBUHZVNVJAUIHZEFVK
      VAUHWCVMWDEVSVIRFVMWDMVAWBAVIUJVTVISUKOVJAULUMUNUOUPUQURUSUT $.
      $( [5-Jun-2007] $) $( [24-Mar-2007] $)
  $}

  $( Reverse closure law for addition of natural numbers.  Exercise 1 of
     [TakeutiZaring] p. 62 and its converse. $)
  nnarcl $p |- ( ( A e. On /\ B e. On ) ->
               ( ( A +o B ) e. om <-> ( A e. om /\ B e. om ) ) ) $=
    ( con0 wcel wa coa co com wss wi oaword1 word eloni ordom ordtr2 mpan2 syl
    exp3a adantr mpd oaword2 ancoms adantl jcad nnacl impbid1 ) ACDZBCDZEZABFGZ
    HDZAHDZBHDZEUIUKULUMUIAUJIZUKULJZABKUGUNUOJUHUGUNUKULUGALZUNUKEULJZAMUPHLZU
    QNAUJHOPQRSTUIBUJIZUKUMJZUHUGUSBAUAUBUHUSUTJUGUHUSUKUMUHBLZUSUKEUMJZBMVAURV
    BNBUJHOPQRUCTUDABUEUF $.
    $( [14-Dec-2004] $) $( [12-Dec-2004] $)

  ${
    $d x A $.  $d x y z B $.
    $( Addition of natural numbers is commutative.  Theorem 4K(2) of
       [Enderton] p. 81. $)
    nnacom $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) = ( B +o A ) ) $=
      ( vx vy vz com wcel coa co wceq cv wi c0 csuc opreq1 opreq2 eqeq12d
      imbi2d nna0r nna0 eqtr4d wa suceq syl peano2b sylbi con0 oasuc nnont
      suceloni syl2an anim12i 3syl syl5bir expcom a2d finds imp nnasuc ) AFGBFG
      ZABHIZBAHIZJZUTCKZBHIZBVDHIZJZLUTMBHIZBMHIZJZLUTDKZBHIZBVKHIZJZLUTVKNZBHI
      ZBVOHIZJZLUTVCLCDAVDMJZVGVJUTVSVEVHVFVIVDMBHOVDMBHPQRVDVKJZVGVNUTVTVEVLVF
      VMVDVKBHOVDVKBHPQRVDVOJZVGVRUTWAVEVPVFVQVDVOBHOVDVOBHPQRVDAJZVGVCUTWBVEVA
      VFVBVDABHOVDABHPQRUTVHBVIBSBTUAVKFGZUTVNVRUTWCVNVRLUTWCUBZVRVLNZVMNZJVNWD
      VPWEVQWFUTWCVPWEJZWCVOVDHIZVKVDHIZNZJZLWCVOMHIZVKMHIZNZJZLWCVOEKZHIZVKWPH
      IZNZJZLWCVOWPNZHIZVKXAHIZNZJZLWCWGLCEBVSWKWOWCVSWHWLWJWNVDMVOHPVSWIWMJWJW
      NJVDMVKHPWIWMUCUDQRVDWPJZWKWTWCXFWHWQWJWSVDWPVOHPXFWIWRJWJWSJVDWPVKHPWIWR
      UCUDQRVDXAJZWKXEWCXGWHXBWJXDVDXAVOHPXGWIXCJWJXDJVDXAVKHPWIXCUCUDQRVDBJZWK
      WGWCXHWHVPWJWEVDBVOHPXHWIVLJWJWEJVDBVKHPWIVLUCUDQRWCWLVOWNWCVOFGWLVOJVKUE
      VOTUFWCWMVKJWNVOJVKTWMVKUCUDUAWPFGZWCWTXEWCXIWTXELWCXIUBZXEWQNZWSNZJWTXJX
      BXKXDXLVOUGGZWPUGGZXBXKJWCXIVOWPUHWCVKUGGZXMVKUIZVKUJUDWPUIZUKXJXOXNUBXCW
      SJXDXLJWCXOXIXNXPXQULVKWPUHXCWSUCUMQWQWSUCUNUOUPUQURBVKUSQVLVMUCUNUOUPUQU
      R $.
      $( [6-May-1995] $)
  $}

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers. $)
  nnaordi $p |- ( ( B e. om /\ C e. om ) ->
               ( A e. B -> ( C +o A ) e. ( C +o B ) ) ) $=
    ( con0 wcel coa co wi com oaordi nnont syl2an ) BDECDEABECAFGCBFGEHBIECIEAB
    CJBKCKL $.
    $( [3-Feb-1996] $)

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring] p. 58,
     limited to natural numbers, and its converse. $)
  nnaord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( C +o A ) e. ( C +o B ) ) ) $=
    ( con0 wcel coa co wb com oaord nnont syl3an ) ADEBDECDEABECAFGCBFGEHAIEBIE
    CIEABCJAKBKCKL $.
    $( [7-Mar-1996] $)

  $( Ordering property of addition of natural numbers. $)
  nnaordr $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( A +o C ) e. ( B +o C ) ) ) $=
    ( com wcel w3a coa co nnaord wceq nnacom ancoms 3adant2 3adant1 eleq12d
    bitrd ) ADEZBDEZCDEZFZABECAGHZCBGHZEACGHZBCGHZEABCITUAUCUBUDQSUAUCJZRSQUECA
    KLMRSUBUDJZQSRUFCBKLNOP $.
    $( [9-Nov-2002] $)

  $( Addition of natural numbers is associative.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/3
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case..)  Theorem 4K(1) of [Enderton] p. 81. $)
  nnaass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A +o B ) +o C ) = ( A +o ( B +o C ) ) ) $=
    ( con0 wcel coa co wceq com oaass nnont syl3an ) ADEBDECDEABFGCFGABCFGFGHAI
    EBIECIEABCJAKBKCKL $.
    $( [20-Sep-1995] $)

  $( Distributive law for natural numbers.  (For brevity, this is just a
     special case of the proof for ordinals.  A direct proof would be about 1/4
     the size of the ordinal proof, since it would use finite induction and not
     require the limit ordinal case.)  Theorem 4K(3) of [Enderton] p. 81. $)
  nndi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( A .o ( B +o C ) ) = ( ( A .o B ) +o ( A .o C ) ) ) $=
    ( con0 wcel coa co comu wceq com odi nnont syl3an ) ADEBDECDEABCFGHGABHGACH
    GFGIAJEBJECJEABCKALBLCLM $.
    $( [20-Sep-1995] $)

  $( Multiplication of natural numbers is associative.  (For brevity, this is
     just a special case of the proof for ordinals.  A direct proof would be
     about 1/3 the size of the ordinal proof, since it would use finite
     induction and not require the limit ordinal case..)  Theorem 4K(4) of
     [Enderton] p. 81. $)
  nnmass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A .o B ) .o C ) = ( A .o ( B .o C ) ) ) $=
    ( con0 wcel comu co wceq com omass nnont syl3an ) ADEBDECDEABFGCFGABCFGFGHA
    IEBIECIEABCJAKBKCKL $.
    $( [20-Sep-1995] $)

  ${
    $d x y A $.  $d x B $.
    $( Multiplication with successor.  Exercise 16 of [Enderton] p. 82. $)
    nnmsucr $p |- ( ( A e. om /\ B e. om ) ->
                  ( suc A .o B ) = ( ( A .o B ) +o B ) ) $=
      ( vx vy com wcel csuc comu co coa wceq cv wi c0 opreq2 id opreq12d
      eqeq12d imbi2d peano2b nnm0 sylbi opreq1d peano1 nna0 ax-mp syl6eq
      eqtr4d wa nnmsuc sylanb nnacom suceq syl nnasuc ancoms 3eqtr4d opreq2d
      nnaass syl3an3b nnmcl syl3an1 3expb anidms an42s eqtrd opreq1 syl5bir
      expcom a2d finds impcom ) BEFAEFZAGZBHIZABHIZBJIZKZVMVNCLZHIZAVSHIZVSJIZK
      ZMVMVNNHIZANHIZNJIZKZMVMVNDLZHIZAWHHIZWHJIZKZMVMVNWHGZHIZAWMHIZWMJIZKZMVM
      VRMCDBVSNKZWCWGVMWRVTWDWBWFVSNVNHOWRWAWEVSNJVSNAHOWRPQRSVSWHKZWCWLVMWSVTW
      IWBWKVSWHVNHOWSWAWJVSWHJVSWHAHOWSPQRSVSWMKZWCWQVMWTVTWNWBWPVSWMVNHOWTWAWO
      VSWMJVSWMAHOWTPQRSVSBKZWCVRVMXAVTVOWBVQVSBVNHOXAWAVPVSBJVSBAHOXAPQRSVMWDN
      WFVMVNEFZWDNKATZVNUAUBVMWFNNJIZNVMWENNJAUAUCNEFXDNKUDNUEUFUGUHWHEFZVMWLWQ
      VMXEWLWQMVMXEUIZWQWIVNJIZWKVNJIZKWLXFWNXGWPXHXBXEWNXGKVMVNWHUJXCUKXFWPWJA
      JIZWMJIZXHXFWOXIWMJAWHUJUCXFWJAWMJIZJIZWJWHVNJIZJIZXJXHXFXKXMWJJXFAWHJIZG
      ZWHAJIZGZXKXMXFXOXQKXPXRKAWHULXOXQUMUNAWHUOXEVMXMXRKWHAUOUPUQURXFXJXLKZXF
      VMXEXSWJEFZVMXEXSXFXTVMWMEFXSXEWJAWMUSWHTUTAWHVAZVBVCVDXFXHXNKZVMXEXEVMYB
      XFXEVMYBXTXEVMYBXFXTXEXBYBVMWJWHVNUSXCUTYAVBVCVEVDUQVFRWIWKVNJVGVHVIVJVKV
      L $.
      $( [21-Sep-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $( Multiplication of natural numbers is commutative.  Theorem 4K(5) of
       [Enderton] p. 81. $)
    nnmcom $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) = ( B .o A ) ) $=
      ( vx vy com wcel comu co wceq cv wi c0 csuc opreq1 opreq2 eqeq12d imbi2d
      nnm0r nnm0 eqtr4d wa coa nnmsucr nnmsuc ancoms syl5bir ex a2d finds imp
      ) AEFBEFZABGHZBAGHZIZUKCJZBGHZBUOGHZIZKUKLBGHZBLGHZIZKUKDJZBGHZBVBGHZIZKU
      KVBMZBGHZBVFGHZIZKUKUNKCDAUOLIZURVAUKVJUPUSUQUTUOLBGNUOLBGOPQUOVBIZURVEUK
      VKUPVCUQVDUOVBBGNUOVBBGOPQUOVFIZURVIUKVLUPVGUQVHUOVFBGNUOVFBGOPQUOAIZURUN
      UKVMUPULUQUMUOABGNUOABGOPQUKUSLUTBRBSTVBEFZUKVEVIVNUKVEVIKVNUKUAZVIVCBUBH
      ZVDBUBHZIVEVOVGVPVHVQVBBUCUKVNVHVQIBVBUDUEPVCVDBUBNUFUGUHUIUJ $.
      $( [21-Sep-1995] $)
  $}

  $( Cancellation law for addition of natural numbers. $)
  nnacan $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                   ( ( A +o B ) = ( A +o C ) <-> B = C ) ) $=
    ( con0 wcel coa co wceq wb com oacan nnont syl3an ) ADEBDECDEABFGACFGHBCHIA
    JEBJECJEABCKALBLCLM $.
    $( [27-Oct-1995] $)

  $( Weak ordering property of addition. $)
  nnaword $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                ( A (_ B <-> ( C +o A ) (_ ( C +o B ) ) ) $=
    ( con0 wcel wss coa co wb com oaword nnont syl3an ) ADEBDECDEABFCAGHCBGHFIA
    JEBJECJEABCKALBLCLM $.
    $( [17-Sep-1995] $)

  $( Weak ordering property of addition. $)
  nnaword1 $p |- ( ( A e. om /\ B e. om ) -> A (_ ( A +o B ) ) $=
    ( con0 wcel coa co wss com oaword1 nnont syl2an ) ACDBCDAABEFGAHDBHDABIAJBJ
    K $.
    $( [9-Nov-2002] $)

  $( Weak ordering property of addition. $)
  nnaword2 $p |- ( ( A e. om /\ B e. om ) -> A (_ ( B +o A ) ) $=
    ( com wcel wa coa co nnaword1 nnacom sseqtrd ) ACDBCDEAABFGBAFGABHABIJ $.
    $( [9-Nov-2002] $)

  $( Ordering property of multiplication.  Half of Proposition 8.19 of
     [TakeutiZaring] p. 63, limited to natural numbers. $)
  nnmordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) -> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel c0 wa comu co wi con0 omordi ex nnont syl2an com23 imp3a
    3adant1 ) BDEZCDEZABEZFCEZGCAHICBHIEZJADESTGZUAUBUCUDUBUAUCBKEZCKEZUBUAUCJZ
    JSTUEUFGUBUGABCLMBNCNOPQR $.
    $( [18-Sep-1995] $)

  $( Ordering property of multiplication.  Proposition 8.19 of
     [TakeutiZaring] p. 63, limited to natural numbers. $)
  nnmord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) <-> ( C .o A ) e. ( C .o B ) ) ) $=
    ( con0 wcel c0 wa comu co wb com omord nnont syl3an ) ADEBDECDEABEFCEGCAHIC
    BHIEJAKEBKECKEABCLAMBMCMN $.
    $( [22-Jan-1996] $)

  $( Cancellation law for multiplication of natural numbers. $)
  nnmcan $p |- ( ( ( A e. om /\ B e. om /\ C e. om ) /\ (/) e. A ) ->
                   ( ( A .o B ) = ( A .o C ) <-> B = C ) ) $=
    ( con0 wcel c0 comu co wceq wb com omcan nnont syl3anl ) ADEBDECDEFAEABGHAC
    GHIBCIJAKEBKECKEABCLAMBMCMN $.
    $( [26-Oct-1995] $)

  ${
    $d x A $.  $d x B $.
    $( Equivalence for ordering.  Compare Exercise 23 of [Enderton] p. 88. $)
    nnaordex $p |- ( ( A e. om /\ B e. om ) -> ( A e. B <->
                  E. x e. om ( (/) e. x /\ ( A +o x ) = B ) ) ) $=
      ( con0 wcel com c0 cv coa co wceq wa wrex wb oaordex nnont sylan2 wi
      eleq1 bicomd nnarcl sylan9bbr pm3.27 syl6bi exp31 com23 adantld com24
      imp4b a1i jcad anim1i impbid1 rexbidv2 bitrd sylan ) BDEZCFEZBCEZGAHZEZBU
      TIJZCKZLZAFMZNBFEZUQURLZUSVDADMZVEUQCDEUSVHNURABCOCPQVGVDVDADFVGUTDEZVDLZ
      UTFEZVDLVGVJVKVDUQURVIVDVKUQVDVIURVKUQVCVIURVKRZRVAUQVIVCVLUQVIVCVLUQVILZ
      VCLURVFVKLZVKVCURVBFEZVMVNVCVOURVBCFSTBUTUAUBVFVKUCUDUEUFUGUHUIVJVDRVGVIV
      DUCUJUKVKVIVDUTPULUMUNUOBPUP $.
      $( [5-Dec-1995] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for weak ordering of natural numbers. $)
    nnawordex $p |- ( ( A e. om /\ B e. om ) -> ( A (_ B <->
                  E. x e. om ( A +o x ) = B ) ) $=
      ( con0 wcel com wss cv coa co wceq wrex wb wa oawordex nnont sylan2 wi
      eleq1 bicomd nnarcl sylan9bbr pm3.27 syl6bi exp31 com23 com24 imp4b a1i
      jcad anim1i impbid1 rexbidv2 bitrd sylan ) BDEZCFEZBCGZBAHZIJZCKZAFLZMBFE
      ZUPUQNZURVAADLZVBUPCDEURVEMUQABCOCPQVDVAVAADFVDUSDEZVANZUSFEZVANVDVGVHVAU
      PUQVFVAVHUPVAVFUQVHUPVFVAUQVHRZUPVFVAVIUPVFNZVANUQVCVHNZVHVAUQUTFEZVJVKVA
      VLUQUTCFSTBUSUAUBVCVHUCUDUEUFUGUHVGVARVDVFVAUCUIUJVHVFVAUSPUKULUMUNBPUO
      $.
      $( [8-Nov-2002] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Lemma for ~ oaabs . $)
    oaabslem $p |- ( ( om e. On /\ A e. om ) -> ( A +o om ) = om ) $=
      ( vx com con0 wcel wa coa co wss cv ciun wlim wceq oalim nnont limom
      jctr syl2an wral nnacl word ordom ordelss mpan syl r19.21aiva iunss
      sylibr adantr eqsstrd ancoms oaword2 sylan2 eqssd ) CDEZACEZFACGHZCUPUOUQ
      CIUPUOFUQBCABJZGHZKZCADEZUOCLZFUQUTMUPUOBACDNAOZUOVBPQRUPUTCIZUOUPUSCIZBC
      SVDUPVEBCUPURCEFUSCEZVEAURTCUAVFVEUBCUSUCUDUEUFBCUSCUGUHUIUJUKUOVACUQIUPC
      AULVCUMUN $.
      $( [3-Mar-2005] $) $( [9-Dec-2004] $)

    $( Ordinal addition absorbs a natural number added to the left of a
       transfinite number.  Proposition 8.10 of [TakeutiZaring] p. 59. $)
    oaabs $p |- ( ( ( A e. om /\ B e. On ) /\ om (_ B ) -> ( A +o B ) = B ) $=
      ( vx vy com wcel con0 wss coa co wceq wi wa cvv ssexg ex word ordom
      elong mpbiri syl6com imp cv csuc opreq2 id eqeq12d imbi2d oaabslem oasuc
      nnont sylan suceq sylan9eq exp31 com12 ad2antrr a2d wlim wral ciun cdif
      cun cuni oalim limom jctr syl2an ancoms limuni ax-mp syl6eq eqtr3d
      adantl wn wb ordelon limord eloni ordtri1 mpan 3syl pm5.32da eldif
      syl6bbr imbi1d impexp syl5bbr ralbidv2 iuneq2 uniiun syl6eqr syl6bi
      adantlr adantr uneq12d visset jctl ssequn1 biimp undif2 syl5eq iuneq1
      syl iunxun syl5reqr an1rs ad2ant2r unieqd uniun 3eqtr4d exp43 com23
      bi2.04 ralbii r19.21v bitr syl5ib com4r impcom tfindsg mpdan com3r imp31
      ) AEFZBGFZEBHZABIJZBKZYFYGYEYIYFYGYEYILZYFYGMEGFZYJYFYGYKYGYFENFZYKYGYFYL
      EBGOPYLYKEQZRENSTUAUBYFYKYGYJYEACUCZIJZYNKZLZYEAEIJZEKZLYEADUCZIJZYTKZLZY
      EAYTUDZIJZUUDKZLYJCDBEYNEKZYPYSYEUUGYOYRYNEYNEAIUEUUGUFUGUHYNYTKZYPUUBYEU
      UHYOUUAYNYTYNYTAIUEUUHUFUGUHYNUUDKZYPUUFYEUUIYOUUEYNUUDYNUUDAIUEUUIUFUGUH
      YNBKZYPYIYEUUJYOYHYNBYNBAIUEUUJUFUGUHYKYEYSAUIZPYTGFZYKMEYTHZMYEUUBUUFUUL
      YEUUBUUFLZLYKUUMYEUULUUNYEUULUUBUUFYEUULMUUBUUEUUAUDZUUDAGFZUULUUEUUOKYEA
      YTUJAUKZULUUAYTUMUNUOUPUQURYNUSZEYNHZYKUUMUUCLZDYNUTZYQLZYKUURUUSMZUVBUVC
      UVAYEYKYPUVCYEUUMUUBLZDYNUTZLZYEYKYPLZLUVAUVCYEUVEUVGUVCUVEYEUVGUVCUVEYEY
      KYPUVCUVEMZYEYKMZMZDEUUAVAZDYNEVBZUUAVAZVCZEVDZUVLVDZVCZYOYNUVJUVKUVOUVMU
      VPUVIUVKUVOKUVHUVIYRUVKUVOUUPYKEUSZMYRUVKKYEYKDAEGVEUUQYKUVRVFVGVHUVIYREU
      VOYKYEYSUUKVIUVREUVOKVFEVJVKVLVMVNUVHUVMUVPKZUVIUURUVEUVSUUSUURUVEUVSUURU
      VEUUBDUVLUTZUVSUURUVDUUBDYNUVLUURYTYNFZUUMMZUUBLYTUVLFZUUBLUWAUVDLUURUWBU
      WCUUBUURUWBUWAYTEFVOZMUWCUURUWAUUMUWDUURUWAMUULYTQZUUMUWDVPZYNQUWAUULUURY
      NYTVQYNVRULYTVSYMUWEUWFREYTVTWAWBWCYTYNEWDWEWFUWAUUMUUBWGWHWIUVTUVMDUVLYT
      VAUVPDUVLUUAYTWJDUVLWKWLWMUBWNWOWPUVCYEYOUVNKZUVEYKUURYEUUSUWGUURYEMUUSYO
      DYNUUAVAZUVNYEUURYOUWHKZUUPYNNFZUURMUWIYEUURDAYNNVEUUQUURUWJCWQWRVHVIUUSD
      EUVLVCZUUAVAZUWHUVNUUSUWKYNKUWLUWHKUUSEYNVCZYNUWKUUSUWMYNKEYNWSWTEYNXAXBZ
      DUWKYNUUAXCXDDEUVLUUAXEXFUNXGXHUVCYNUVQKUVEUVIUURUUSYNYNVDZUVQYNVJUUSUWKV
      DUWOUVQUUSUWKYNUWNXIEUVLXJXFUNUQXKXLXMURUVAYEUVDLZDYNUTUVFUUTUWPDYNUUMYEU
      UBXNXOYEUVDDYNXPXQXRXSXTXGYAXGYBPYCYD $.
      $( [3-Mar-2005] $) $( [9-Dec-2004] $)
  $}

  $( One is a natural number. $)
  1onn $p |- 1o e. om $=
    ( c1o c0 csuc com df-1o wcel peano1 peano2 ax-mp eqeltr ) ABCZDEBDFKDFGBHIJ
    $.
    $( [29-Oct-1995] $)

  $( The ordinal 2 is a natural number. $)
  2onn $p |- 2o e. om $=
    ( c2o c1o csuc com df-2o wcel 1onn peano2 ax-mp eqeltr ) ABCZDEBDFKDFGBHIJ
    $.
    $( [6-Oct-2004] $) $( [28-Sep-2004] $)

  ${
    $d x y z A $.
    $( A natural number is even iff its successor is odd. $)
    nneob $p |- ( A e. om ->
      ( E. x e. om A = ( 2o .o x ) <-> -. E. x e. om suc A = ( 2o .o x ) ) ) $=
      ( vy vz com wcel c2o cv comu co wceq wrex csuc wn opreq2 eqeq2d cbvrexv
      wa con0 oneo nnont id syl3an 3com23 3expa nrexdv r19.23aiva sylbi wi c0
      suceq eqeq1d rexbidv negbid eqeq1 imbi12d peano1 eqid 2on om0 ax-mp
      syl6eq rcla4ev mp2an a1i c1o coa 1on oaass mp3an23 o1p1e2 opreq2i
      syl6req oa1suc opreq1d suceloni syl 3eqtrd eqcomd ad2antrr opreq1 2onn
      1onn nndi mp3an13 om1 sylan9eqr adantll eqtrd ex nnacl mpan2 adantl
      jctild syl6 r19.23adva syl5ib con3d con1 syl9 finds impbid2 ) BEFBGAHZIJZ
      KZAELZBMZXDKZAELZNZXFBGCHZIJZKZCELXJXEXMACEXCXKKZXDXLBXCXKGIOZPQXMXJCEXKE
      FZXMRXHAEXPXMXCEFZXHNZXPXQXMXRXKSFXCSFXMXRXPXQXMXKXCBTXKUAXCUAXMUBUCUDUEU
      FUGUHXKMZXDKZAELZNZXKXDKZAELZUIUJMZXDKZAELZNZUJXDKZAELZUIDHZMZXDKZAELZNZY
      KXDKZAELZUIZYLMZXDKZAELZNZYNUIXJXFUICDBXKUJKZYBYHYDYJUUCYAYGUUCXTYFAEUUCX
      SYEXDXKUJUKULUMUNUUCYCYIAEXKUJXDUOUMUPXKYKKZYBYOYDYQUUDYAYNUUDXTYMAEUUDXS
      YLXDXKYKUKULUMUNUUDYCYPAEXKYKXDUOUMUPXKYLKZYBUUBYDYNUUEYAUUAUUEXTYTAEUUEX
      SYSXDXKYLUKULUMUNUUEYCYMAEXKYLXDUOUMUPXKBKZYBXJYDXFUUFYAXIUUFXTXHAEUUFXSX
      GXDXKBUKULUMUNUUFYCXEAEXKBXDUOUMUPYJYHUJEFUJUJKZYJUQUJURYIUUGAUJEXCUJKZXD
      UJUJUUHXDGUJIJZUJXCUJGIOGSFZUUIUJKUSGUTVAVBPVCVDVEYKEFZUUBYQNYRYNUUKYQUUA
      UUKYKXLKZCELUUAYQUUKUULUUACEUUKXPRZUULXKVFVGJZEFZYSGUUNIJZKZRUUAUUMUULUUQ
      UUOUUMUULUUQUUMUULRYSYKGVGJZUUPUUKYSUURKXPUULUUKUURYSUUKYKSFZUURYSKYKUAUU
      SUURYKVFVGJZVFVGJZYLVFVGJZYSUUSUVAYKVFVFVGJZVGJZUURUUSVFSFZUVEUVAUVDKVHVH
      YKVFVFVIVJUVCGYKVGVKVLVMUUSUUTYLVFVGYKVNVOUUSYLSFUVBYSKYKVPYLVNVQVRVQVSVT
      XPUULUURUUPKUUKUULXPUURXLGVGJZUUPYKXLGVGWAXPUUPXLGVFIJZVGJZUVFGEFXPVFEFZU
      UPUVHKWBWCGXKVFWDWEUVGGXLVGUUJUVGGKUSGWFVAVLVMWGWHWIWJXPUUOUUKXPUVIUUOWCX
      KVFWKWLWMWNYTUUQAUUNEXCUUNKXDUUPYSXCUUNGIOPVCWOWPYPUULACEXNXDXLYKXOPQWQWR
      YNYQWSWTXAXB $.
      $( [26-Jan-2006] $) $( [26-Jan-2006] $)
  $}

  ${
    $d x y z w v A $.  $d x y z w v F $.
    $( Lemma for ~ omsmo . $)
    omsmolem $p |- ( y e. om -> ( ( ( A (_ On /\ F : om --> A ) /\
        A. x e. om ( F ` x ) e. ( F ` suc x ) ) ->
       ( z e. y -> ( F ` z ) e. ( F ` y ) ) ) ) $=
      ( vw cv wcel cfv wi c0 csuc con0 wss com wf wa wral wceq eleq2 fveq2
      eleq2d imbi12d noel pm2.21i a1i wo suceq fveq2d eleq12d rcla4cva adantll
      ssel ffvelrn peano2b sylan2b syl5 ontr1 exp3a com23 syl6 imp31 adantlr
      mpd imim2d imp eleq1d syl5cbir adantr jaod visset elsuc syl5ib exp31
      com12 finds2 ) CGZBGZHZVQEIZVREIZHZJVQKHZVTKEIZHZJZVQFGZHZVTWGEIZHZJZVQWG
      LZHZVTWLEIZHZJZDMNZODEPZQZAGZEIZWTLZEIZHZAORZQZBFVRKSZVSWCWBWEVRKVQTXGWAW
      DVTVRKEUAUBUCVRWGSZVSWHWBWJVRWGVQTXHWAWIVTVRWGEUAUBUCVRWLSZVSWMWBWOVRWLVQ
      TXIWAWNVTVRWLEUAUBUCWFXFWCWEVQUDUEUFXFWGOHZWKWPJXFXJWKWPXFXJQZWKQZWHVQWGS
      ZUGWOWMXLWHWOXMXKWKWHWOJXKWJWOWHXKWIWNHZWJWOJZXEXJXNWSXDXNAWGOWTWGSZXAWIX
      CWNWTWGEUAXPXBWLEWTWGUHUIUJUKZULWSXJXNXOJZXEWQWRXJXRWQWRXJXRWQWRXJQZWNMHZ
      XRWQWNDHZXTXSDMWNUMWRWLOHYAXJODWLEUNWGUOUPUQXTWJXNWOXTWJXNWOVTWIWNURUSUTV
      AUSVBVCVDVEVFXKXMWOJZWKXEXJYBWSXMWOXNXEXJQXMVTWIWNVQWGEUAVGXQVHULVIVJVQWG
      CVKVLVMVNVOVP $.
      $( [24-Jun-2004] $) $( [30-Nov-2003] $)

    $( A strictly monotonic ordinal function on the set of natural numbers
       is one-to-one. $)
    omsmo $p |- ( ( ( A (_ On /\ F : om --> A ) /\
          A. x e. om ( F ` x ) e. ( F ` suc x ) ) -> F : om -1-1-> A ) $=
      ( vy vz con0 wss com wf wa cv cfv csuc wcel wral wceq wi wf1 simplr wo
      wn omsmolem adantl imp adantr orim12d ancoms con3d wb word ssel ffvelrn
      syl5 exp3a eloni syl6 anim12d ordtri3 syl adantlr nnord syl2an 3imtr4d
      exp32 r19.21adv r19.21aiv jca f1fv sylibr ) BFGZHBCIZJZAKZCLVMMCLNAHOZJZV
      KDKZCLZEKZCLZPZVPVRPZQZEHOZDHOZJHBCRVOVKWDVJVKVNSVOWCDHVOVPHNZWBEHVOWEVRH
      NZWBVOWEWFJZJZVQVSNZVSVQNZTZUAZVPVRNZVRVPNZTZUAZVTWAWHWOWKWGVOWOWKQWGVOJW
      MWIWNWJWGVOWMWIQZWFVOWQQWEAEDBCUBUCUDWGVOWNWJQZWEVOWRQWFADEBCUBUEUDUFUGUH
      VLWGVTWLUIZVNVLWGJVQUJZVSUJZJZWSVLWGXBVLWEWTWFXAVLWEVQFNZWTVJVKWEXCQVJVKW
      EXCVJVQBNXCVKWEJBFVQUKHBVPCULUMUNUDVQUOUPVLWFVSFNZXAVJVKWFXDQVJVKWFXDVJVS
      BNXDVKWFJBFVSUKHBVRCULUMUNUDVSUOUPUQUDVQVSURUSUTWGWAWPUIZVOVPUJVRUJXEWEWF
      VPVRURVPVAVRVAVBUCVCVDVEVFVGDEHBCVHVI $.
      $( [24-Jun-2004] $) $( [30-Nov-2003] $)
  $}


