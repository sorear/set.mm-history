$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Equinumerosity (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a c $.  $d B a c $.  $d C c $.  $d D a c $.
    xpen.1 $e |- B e. _V $.
    xpen.2 $e |- D e. _V $.
    $( Equinumerosity law for cross product.  Proposition 4.22(b) of
       [Mendelson] p. 254.  (Revised by Mario Carneiro, 9-Mar-2013.) $)
    xpen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) $=
      ( va vc cvv wcel wa cen wbr cxp cdom xpdom2 wb sbthbg ax-mp xpex brrelexi
      relen anim12i cv wi breq1 anbi1d xpeq1 breq1d imbi12d anbi2d xpeq2 visset
      wceq 3imtr3i xpcomen enen2 mp2an enen1 bitri sylib syl2an vtocl2g mpcom
      entr ) AIJZCIJZKABLMZCDLMZKZACNZBDNZLMZVHVFVIVGABLUBUACDLUBUAUCGUDZBLMZHU
      DZDLMZKZVNVPNZVLLMZUEVHVQKZAVPNZVLLMZUEVJVMUEGHACIIVNAUNZVRWAVTWCWDVOVHVQ
      VNABLUFUGWDVSWBVLLVNAVPUHUIUJVPCUNZWAVJWCVMWEVQVIVHVPCDLUFUKWEWBVKVLLVPCA
      ULUIUJVOVSBVPNZLMZWFVLLMZVTVQVOVPVNNZVPBNZLMZWGVNBOMZBVNOMZKZWIWJOMZWJWIO
      MZKZVOWKWLWOWMWPVNBVPEHUMZPBVNVPGUMZWRPUCBIJWNVOQEVNBIRSWJIJWQWKQVPBWRETW
      IWJIRSUOWKWIWFLMZWGWFIJWJWFLMWKWTQBVPEWRTVPBWREUPWJWFWIIUQURVSIJWIVSLMWTW
      GQVNVPWSWRTVPVNWRWSUPWIVSWFIUSURUTVAVPDOMZDVPOMZKZWFVLOMZVLWFOMZKZVQWHXAX
      DXBXEVPDBFEPDVPBWREPUCDIJXCVQQFVPDIRSVLIJXFWHQBDEFTWFVLIRSUOVSWFVLVEVBVCV
      D $.
      $( [9-Mar-2013] $) $( [24-Jul-2004] $)
  $}

  ${
    xpenOLD.1 $e |- A e. _V $.
    xpenOLD.2 $e |- B e. _V $.
    xpenOLD.3 $e |- C e. _V $.
    xpenOLD.4 $e |- D e. _V $.
    $( Equinumerosity law for cross product.  Proposition 4.22(b) of
       [Mendelson] p. 254. $)
    xpenOLD $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) $=
      ( cen wbr cxp cdom wa xpdom2 cvv wcel wb sbthbg ax-mp xpex anim12i syl2an
      3imtr3i xpcomen enen2 mp2an enen1 bitri sylib entr ) ABIJZACKZBCKZIJZUMBD
      KZIJZULUOIJCDIJZUKCAKZCBKZIJZUNABLJZBALJZMZURUSLJZUSURLJZMZUKUTVAVDVBVEAB
      CFGNBACEGNUABOPVCUKQFABORSUSOPVFUTQCBGFTURUSORSUCUTURUMIJZUNUMOPUSUMIJUTV
      GQBCFGTCBGFUDUSUMUROUEUFULOPURULIJVGUNQACEGTCAGEUDURULUMOUGUFUHUICDLJZDCL
      JZMZUMUOLJZUOUMLJZMZUQUPVHVKVIVLCDBHFNDCBGFNUADOPVJUQQHCDORSUOOPVMUPQBDFH
      TUMUOORSUCULUMUOUJUB $.
      $( [24-Jul-2004] $)
  $}

  ${
    $d A b d $.  $d B b d $.  $d C b d $.  $d D d $.
    $( Equinumerosity law for cross product.  (Contributed by Jeff Madsen,
       17-Jun-2010.) $)
    xpeng $p |- ( ( B e. V /\ D e. W ) ->
                    ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) ) $=
      ( vb vd cv cen wbr wa cxp wi wceq breq2 anbi1d breq2d imbi12d visset xpen
      xpeq1 anbi2d xpeq2 vtocl2g ) AGIZJKZCHIZJKZLZACMZUFUHMZJKZNABJKZUILZUKBUH
      MZJKZNUNCDJKZLZUKBDMZJKZNGHBDEFUFBOZUJUOUMUQVBUGUNUIUFBAJPQVBULUPUKJUFBUH
      UBRSUHDOZUOUSUQVAVCUIURUNUHDCJPUCVCUPUTUKJUHDBUDRSAUFCUHGTHTUAUE $.
      $( [17-Jun-2010] $)
  $}

  ${
    $d A a b $.  $d B b $.  $d C a b c d $.  $d D a b d $.  $d X a b $.
    $d Y a b $.
    $( Equinumerosity law for cross product.  (Contributed by Jeff Madsen,
       17-Jun-2010.) $)
    xpengOLDOLD $p |- ( ( ( A e. V /\ B e. W ) /\ ( C e. X /\ D e. Y ) ) ->
                    ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) ) $=
      ( va vb vd wcel wa cen wbr cxp wi cv wceq imbi12d vc anbi1d breq1d imbi2d
      breq1 xpeq1 breq2 breq2d anbi2d xpeq2 visset xpen vtocl2g imp ) AELBFLMCG
      LDHLMZABNOZCDNOZMZACPZBDPZNOZQZUOIRZJRZNOZUQMZVCCPZVDDPZNOZQZQUOAVDNOZUQM
      ZUSVHNOZQZQUOVBQIJABEFVCASZVJVNUOVOVFVLVIVMVOVEVKUQVCAVDNUEUBVOVGUSVHNVCA
      CUFUCTUDVDBSZVNVBUOVPVLURVMVAVPVKUPUQVDBANUGUBVPVHUTUSNVDBDUFUHTUDVEUARZK
      RZNOZMZVCVQPZVDVRPZNOZQVECVRNOZMZVGWBNOZQVJUAKCDGHVQCSZVTWEWCWFWGVSWDVEVQ
      CVRNUEUIWGWAVGWBNVQCVCUJUCTVRDSZWEVFWFVIWHWDUQVEVRDCNUGUIWHWBVHVGNVRDVDUJ
      UHTVCVDVQVRJUKKUKULUMUMUN $.
      $( [17-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The cartesian product of numerable sets is numerable.  (Contributed by
       Mario Carneiro, 3-Mar-2013.) $)
    xpnum $p |- ( ( E. x e. On x ~~ A /\ E. x e. On x ~~ B ) ->
      E. x e. On x ~~ ( A X. B ) ) $=
      ( vy vz cv cen wbr con0 wrex cxp breq1 cbvrexv wa cvv wcel wi syl11anc c0
      imp w3a comu co simp1l simp2l omxpen simp3l simp3r simp1r simp2r syl22anc
      omcl xpeng entr rcla4ev 3expia exp31 r19.23aiv exp3a r19.23adv wn wne 0ex
      0elon enref brprc mpbiri sylancr con1i wceq onenon ax-mp rexbidv necon3bi
      breq2 xpexr2 pm2.61d1 syl2anb ) AFZBGHZAIJDFZBGHZDIJZEFZCGHZEIJZVSBCKZGHZ
      AIJZVSCGHZAIJVTWBADIVSWABGLMWJWEAEIVSWDCGLMWCWFNBOPZCOPZNZWIWCWFWMWIQZWCW
      EWNEIWCWDIPZWEWNWBWOWENZWNQDIWAIPZWBWPWNWQWBNZWPWMWIWRWPWMUAZWAWDUBUCZIPZ
      WTWGGHZWIWSWQWOXAWQWBWPWMUDZWRWOWEWMUEZWAWDULRWSWTWAWDKZGHZXEWGGHZXBWSWQW
      OXFXCXDWAWDUFRWSWKWLWBWEXGWRWPWKWLUGWRWPWKWLUHWQWBWPWMUIWRWOWEWMUJWMWBWEN
      XGWABWDCOOUMTUKWTXEWGUNRWHXBAWTIVSWTWGGLUORUPUQURUSUTTWIWMWIVAWGOPZWGSVBW
      MXHWIXHVAZSIPZSWGGHZWIVDXIXKSSGHSVCVESWGGVFVGWHXKASIVSSWGGLUOVHVIWIWGSWGS
      VJZWIVSSGHZAIJZXJXNVDASVKVLXLWHXMAIWGSVSGVOVMVGVNBCOVPRVIVQVR $.
      $( [3-Mar-2013] $)
  $}

  ${
    $d f g x y z w v A $.  $d f g x y z w v B $.  $d f g x y z w v C $.
    $d f g x y z w v D $.  $d z w v H $.
    mapenlem.1 $e |- A e. _V $.
    mapenlem.2 $e |- B e. _V $.
    mapenlem.3 $e |- C e. _V $.
    mapenlem.4 $e |- D e. _V $.
    mapenlem.5 $e |- H =
         { <. x , y >. | ( x e. ( A ^m C ) /\ y = ( ( f o. x ) o. `' g ) ) } $.
    $( Lemma for ~ mapenOLD . $)
    mapenlem1 $p |- ( ( ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) /\ z :
    C --> A ) /\ v e. C ) -> ( ( H ` z ) ` ( g ` v ) ) = ( f ` ( z ` v ) ) ) $=
      ( wa cfv ccom wceq cv wf1o wf wcel ccnv cmap co elmap coeq2 coeq1d visset
      coex cnvex fvopab4 sylbir fveq1d ad2antlr coass f1of sylan cres f1ococnv1
      fco cid coeq2d fcoi1 sylan9eqr an32s syl5eq adantr wfun f1ofun ffun funco
      syl2an dff1o3 simprbi anim12i anabss3 fvco3 3expa adantlr anassrs 3eqtr3d
      wfo 3expb eqtrd ) EFIUAZUBZGHJUAZUBZQZGECUAZUCZQZDUAZGUDZQZWPWJRZWMKRZRZW
      SWHWMSZWJUEZSZRZWPWMRWHRZWNXAXETWLWQWNWSWTXDWNWMEGUFUGZUDWTXDTEGWMLNUHABW
      MWHAUAZSZXCSXDXGKXHWMTXIXBXCXHWMWHUIUJPXBXCWHWMIUKCUKULWJJUKUMULUNUOUPUQW
      RWPXDWJSZRZWPXBRZXEXFWOXKXLTWQWOWPXJXBWOXJXBXCWJSZSZXBXBXCWJURWIWNWKXNXBT
      ZWIWNQZGFXBUCZWKXOWIEFWHUCWNXQEFWHUSGEFWHWMVCUTWKXQXNXBVDGVAZSXBWKXMXRXBG
      HWJVBVEGFXBVFVGUTVHVIUPVJWOXDVKZGHWJUCZQZWQXKXETZWIWNWKYAXPWKYAXPWKQXSWKX
      TXPXBVKZXCVKZXSWKWIWHVKZWMVKYCWNEFWHVLZGEWMVMWHWMVNVOWKGHWJWEYDGHWJVPVQXB
      XCVNVOGHWJUSVRVSVHXSXTWQYBGHWPXDWJVTWAUTWLWNWQXLXFTZWIWNWQQZYGWKWIYEYHYGY
      FYEWNWQYGGEWPWHWMVTWFUTWBWCWDWG $.
      $( [16-Dec-2003] $)

    $( Lemma for ~ mapenOLD . $)
    mapenlem2 $p |- ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) ->
             H : ( A ^m C ) -1-1-onto-> ( B ^m D ) ) $=
      ( wa wceq wf cfv ccom wcel vz vw vv cv wf1o cmap co wf1 wi wral ccnv f1of
      crn fco sylan f1ocnv syl syl2an exp31 com23 elmap 3imtr4g r19.21aiv sylib
      imp fopab2 anbi12i fveq1 ad2antlr mapenlem1 adantrl exp43 adantrd adantld
      adantl imp42 3eqtr3d f1of1 ffvelrn adantlr adantll jca anassrs r19.21aiva
      f1fveq mpbid wfn ffn eqfnfv2 ad2antrl mpbird exp32 syl5bi exp3a r19.21adv
      wb dff13 sylanbrc wss wrex an32s sylibr coeq2 coeq1d visset cnvex fvopab4
      frn coex coass coeq2i coeq1i eqtr2i 3eqtri cres f1ococnv2 sylan9eq coeq2d
      cid fcoi2 fcoi1 eqtrd syl5eq ex fveq2 eqeq1d rcla4ev syl6 fvelrnb sylibrd
      3syl ssrdv eqssd dff1o5 ) CDGUDZUEZEFHUDZUEZOZCEUFUGZDFUFUGZIUHZIUMZUUAPY
      TUUAIUEYSYTUUAIQZUAUDZIRZUBUDZIRZPZUUEUUGPZUIZUBYTUJZUAYTUJUUBYSYOAUDZSZY
      QUKZSZUUATZAYTUJUUDYSUUQAYTYSECUUMQZFDUUPQZUUMYTTUUQYPYRUURUUSUIYPUURYRUU
      SYPUURYRUUSYPUUROEDUUNQZFEUUOQZUUSYRYPCDYOQUURUUTCDYOULECDYOUUMUNUOYRFEUU
      OUEUVAEFYQUPFEUUOULUQFEDUUNUUOUNURUSUTVECEUUMJLVADFUUPKMVAVBVCABYTUUAUUPI
      NVFVDZYSUULUAYTYSUUEYTTZUUKUBYTYSUVCUUGYTTZUUKUVCUVDOECUUEQZECUUGQZOZYSUU
      KUVCUVEUVDUVFCEUUEJLVACEUUGJLVAVGYSUVGUUIUUJYSUVGUUIOZOZUUJUCUDZUUERZUVJU
      UGRZPZUCEUJZUVIUVMUCEUVIUVJETZOZUVKYORZUVLYORZPZUVMUVPUVJYQRZUUFRZUVTUUHR
      ZUVQUVRUVHUWAUWBPZYSUVOUUIUWCUVGUVTUUFUUHVHVOVIYSUVGUUIUVOUWAUVQPZYSUVEUU
      IUVOUWDUIUIUVFYSUVEUUIUVOUWDYSUVEOUVOUWDUUIABUAUCCDEFGHIJKLMNVJVKVLVMVPYS
      UVGUUIUVOUWBUVRPZYSUVFUUIUVOUWEUIUIUVEYSUVFUUIUVOUWEYSUVFOUVOUWEUUIABUBUC
      CDEFGHIJKLMNVJVKVLVNVPVQYSUVHUVOUVSUVMWPZYPUVHUVOOZUWFYRYPCDYOUHUVKCTZUVL
      CTZOZUWFUWGCDYOVRUVGUVOUWJUUIUVGUVOOUWHUWIUVEUVOUWHUVFECUVJUUEVSVTUVFUVOU
      WIUVEECUVJUUGVSWAWBVTCDUVKUVLYOWEURVTWCWFWDUVGUUJUVNWPZYSUUIUVEUUEEWGUUGE
      WGUWKUVFECUUEWHECUUGWHUCEUUEUUGWIURWJWKWLWMWNWOVCUAUBYTUUAIWQWRYSUUCUUAYS
      UUDUUCUUAWSUVBYTUUAIXHUQYSUAUUAUUCYSUUEUUATZUUHUUEPZUBYTWTZUUEUUCTZYSUWLY
      OUKZUUESZYQSZYTTZUWRIRZUUEPZOZUWNUWLFDUUEQZYSUXBDFUUEKMVAYSUXCUXBYSUXCOZU
      WSUXAUXDECUWRQZUWSYPUXCYRUXEYPUXCOZFCUWQQZEFYQQUXEYRYPDCUWPQZUXCUXGYPDCUW
      PUEUXHCDYOUPDCUWPULUQFDCUWPUUEUNUOEFYQULEFCUWQYQUNURXACEUWRJLVAXBZUXDUWTY
      OUWRSZUUOSZUUEUXDUWSUWTUXKPUXIABUWRUUPUXKYTIUUMUWRPUUNUXJUUOUUMUWRYOXCXDN
      UXJUUOYOUWRGXEZUWQYQUWPUUEYOUXLXFUAXEXIHXEZXIXIYQUXMXFXIXGUQUXDUXKYOUWPSZ
      UUESZYQUUOSZSZUUEUXKYOUWRUUOSZSYOUWQUXPSZSZUXQYOUWRUUOXJUXRUXSYOUWQYQUUOX
      JXKUXQYOUWQSZUXPSUXTUXOUYAUXPYOUWPUUEXJXLYOUWQUXPXJXMXNUXDUXQUUEUXPSZUUEY
      PUXCUXQUYBPYRUXFUXOUUEUXPYPUXCUXOXSDXOZUUESUUEYPUXNUYCUUECDYOXPXDFDUUEXTX
      QXDVTYRUXCUYBUUEPYPYRUXCUYBUUEXSFXOZSUUEYRUXPUYDUUEEFYQXPXRFDUUEYAXQWAYBY
      CYBWBYDWMUWMUXAUBUWRYTUUGUWRPUUHUWTUUEUUGUWRIYEYFYGYHYSUUDIYTWGUWOUWNWPUV
      BYTUUAIWHUBYTUUEIYIYKYJYLYMYTUUAIYNWR $.
      $( [16-Dec-2003] $)
  $}

  ${
    $d A a c $.  $d B a c f g x y $.  $d C c $.  $d D a c f g x y $.
    mapen.1 $e |- B e. _V $.
    mapen.2 $e |- D e. _V $.
    $( Two set exponentiations are equinumerous when their bases and exponents
       are equinumerous.  Theorem 6H(c) of [Enderton] p. 139.  (Revised by
       Mario Carneiro, 9-Mar-2013.) $)
    mapen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A ^m C ) ~~ ( B ^m D ) ) $=
      ( va vc vf vg vx vy cvv wa cen wbr cmap co cv wi wcel relen brrelexi wceq
      anim12i breq1 anbi1d opreq1 breq1d imbi12d anbi2d wf1o wex bren ccom ccnv
      opreq2 copab visset eqid mapenlem2 oprex syl ex 19.23aiv 19.23adv syl2anb
      f1oen imp vtocl2g mpcom ) AMUAZCMUAZNABOPZCDOPZNZACQRZBDQRZOPZVNVLVOVMABO
      UBUCCDOUBUCUEGSZBOPZHSZDOPZNZVTWBQRZVROPZTVNWCNZAWBQRZVROPZTVPVSTGHACMMVT
      AUDZWDWGWFWIWJWAVNWCVTABOUFUGWJWEWHVROVTAWBQUHUIUJWBCUDZWGVPWIVSWKWCVOVNW
      BCDOUFUKWKWHVQVROWBCAQUQUIUJWAVTBISZULZIUMZWBDJSZULZJUMZWFWCVTBIEUNWBDJFU
      NWNWQWFWNWPWFJWMWPWFTIWMWPWFWMWPNWEVRKSZWEUALSWLWRUOWOUPUOUDNKLURZULWFKLV
      TBWBDIJWSGUSEHUSFWSUTVAWEVRWSVTWBQVBVHVCVDVEVFVIVGVJVK $.
      $( [9-Mar-2013] $) $( [16-Dec-2003] $)
  $}

  ${
    $d f g x y A $.  $d f g x y B $.  $d f g x y C $.  $d f g x y D $.
    mapenOLD.1 $e |- A e. _V $.
    mapenOLD.2 $e |- B e. _V $.
    mapenOLD.3 $e |- C e. _V $.
    mapenOLD.4 $e |- D e. _V $.
    $( Two set exponentiations are equinumerous when their bases and exponents
       are equinumerous.  Theorem 6H(c) of [Enderton] p. 139. $)
    mapenOLD $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A ^m C ) ~~ ( B ^m D ) ) $=
      ( vf vg vx vy cen wbr cv wf1o wex cmap co bren wi wa wcel ccom ccnv copab
      wceq eqid mapenlem2 oprex f1oen syl ex 19.23aiv 19.23adv imp syl2anb ) AB
      MNABIOZPZIQZCDJOZPZJQZACRSZBDRSZMNZCDMNABIFTCDJHTUTVCVFUTVBVFJUSVBVFUAIUS
      VBVFUSVBUBVDVEKOZVDUCLOURVGUDVAUEUDUGUBKLUFZPVFKLABCDIJVHEFGHVHUHUIVDVEVH
      ACRUJUKULUMUNUOUPUQ $.
      $( [16-Dec-2003] $)
  $}

  ${
    $d A a $.  $d B a x $.  $d C a x $.
    mapdom1.1 $e |- B e. _V $.
    mapdom1.2 $e |- C e. _V $.
    $( Order-preserving property of set exponentiation.  Theorem 6L(c) of
       [Enderton] p. 149.  (Revised by Mario Carneiro, 9-Mar-2013.) $)
    mapdom1 $p |- ( A ~<_ B -> ( A ^m C ) ~<_ ( B ^m C ) ) $=
      ( va vx cvv wcel cdom wbr cmap co reldom brrelexi cv wi wceq cen wss wex
      breq1 opreq1 breq1d imbi12d wa domen enref visset mapen mpan2 oprex mapss
      ssdomg mpsyl endomtr syl2an 19.23aiv sylbi vtoclg mpcom ) AHIABJKZACLMZBC
      LMZJKZABJNOFPZBJKZVFCLMZVDJKZQVBVEQFAHVFARZVGVBVIVEVFABJUBVJVHVCVDJVFACLU
      CUDUEVGVFGPZSKZVKBTZUFZGUAVIGVFBDUGVNVIGVLVHVKCLMZSKZVOVDJKZVIVMVLCCSKVPC
      EUHVFVKCCGUIEUJUKVOHIVMVOVDTVQVKCLULVKBCDEUMVOVDHUNUOVHVOVDUPUQURUSUTVA
      $.
      $( [9-Mar-2013] $) $( [27-Jul-2004] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.
    mapdom2.1 $e |- A e. _V $.
    mapdom2.2 $e |- B e. _V $.
    mapdom2.3 $e |- C e. _V $.
    $( Order-preserving property of set exponentiation.  Theorem 6L(c) of
       [Enderton] p. 149. $)
    mapdom1OLD $p |- ( A ~<_ B -> ( A ^m C ) ~<_ ( B ^m C ) ) $=
      ( vx cdom wbr cv cen wss wa wex cmap co domen enref visset cvv mapen wcel
      mpan2 oprex mapss ssdomg mpsyl endomtr syl2an 19.23aiv sylbi ) ABHIAGJZKI
      ZULBLZMZGNACOPZBCOPZHIZGABEQUOURGUMUPULCOPZKIZUSUQHIZURUNUMCCKIUTCFRAULCC
      GSFUAUCUSTUBUNUSUQLVAULCOUDULBCEFUEUSUQTUFUGUPUSUQUHUIUJUK $.
      $( [27-Jul-2004] $)

    $( Lemma for ~ mapdom2 . $)
    mapdom2lem $p |- ( x e. ( C ^m z ) ->
                     ( x i^i ( ( B \ z ) X. { w } ) ) = (/) ) $=
      ( cv cmap cin cdm c0 wceq visset wss syl wrel ax-mp co wcel csn cxp elmap
      cdif wf fdm sylbi fconst fdmi a1i ineq12d disjdif syl6eq dmin sseq2 mpbii
      ss0 wb relxp relin1 incom releqi mpbi reldm0 sylibr ) AJZFBJZKUAUBZVHEVIU
      FZCJZUCZUDZLZMZNOZVONOZVJVHMZVNMZLZNOZVQVJWAVIVKLNVJVSVIVTVKVJVIFVHUGVSVI
      OFVIVHIBPUEVIFVHUHUIVTVKOVJVKVMVNVKVLCPUJUKULUMVIEUNUOWBVPNQZVQWBVPWAQWCV
      HVNUPWANVPUQURVPUSRRVOSZVRVQUTVNVHLZSZWDVNSWFVKVMVAVNVHVBTWEVOVNVHVCVDVEV
      OVFTVG $.
      $( [23-Sep-2004] $)

    $( Order-preserving property of set exponentiation.  Theorem 6L(d) of
       [Enderton] p. 149. $)
    mapdom2 $p |- ( ( A ~<_ B /\ -. ( A = (/) /\ C = (/) ) ) ->
                  ( C ^m A ) ~<_ ( C ^m B ) ) $=
      ( vw vz vx c0 wceq wa cdom wbr cmap co cv wcel cun wf vy wn exmid biantru
      wo ianor ordir 3bitr4i wi opreq1 wne df-ne sylbir sylan9eqr 0dom syl6eqbr
      map0b a1i wex neq0 cen wss domen enref visset mapen mpan cvv cdif csn cxp
      oprex fconst cin disjdif fun mpan2 wb undif feq2 sylbi snssi ssequn2 feq3
      sylib syl sylan9bb syl5ib elmap 3imtr4g mapdom2lem eqcomd sylan9eq unineq
      biantrud syl6bb mpi endomtr syl2an expr 19.23aiv 19.23adv syl5bi jaod imp
      dom2d sylan2b ) AJKZCJKZLUBZABMNZXHUBZXILZXIUBZUEZCAOPZCBOPZMNZXLXNUEZXSX
      IXNUEZLXJXOXTXSXIUCUDXHXIUFXLXIXNUGUHXKXOXRXKXMXRXNXMXRUIXKXMXPJXQMXIXLXP
      JAOPZJCJAOUJXLAJUKYAJKAJULADUQUMUNXQUOUPURXNGQZCRZGUSXKXRGCUTXKYCXRGXKAHQ
      ZVANZYDBVBZLZHUSYCXRUIZHABEVCYGYHHYEYFYCXRYEXPCYDOPZVANZYIXQMNZXRYFYCLZCC
      VANYEYJCFVDCCAYDFHVEZVFVGYLYIVHRYKCYDOVLYLIUAYIXQIQZBYDVIZYBVJZVKZSZUAQZY
      QSZVHYLYDCYNTZBCYRTZYNYIRZYRXQRUUAYDYOSZCYPSZYRTZYLUUBUUAYOYPYQTZUUFYOYBG
      VEVMUUAUUGLYDYOVNJKUUFYDBVOYDYOCYPYNYQVPVQVQYFUUFBUUEYRTZYCUUBYFUUDBKUUFU
      UHVRYDBVSUUDBUUEYRVTWAYCUUECKZUUHUUBVRYCYPCVBUUIYBCWBYPCWCWEUUECBYRWDWFWG
      WHCYDYNFYMWICBYRFEWIWJUUCYSYIRZLZYRYTKZYNYSKZVRUIYLUUKUULUULYNYQVNZYSYQVN
      ZKZLUUMUUKUUPUULUUCUUJUUNJUUOIHGABCDEFWKUUJUUOJUAHGABCDEFWKWLWMWOYNYSYQWN
      WPURXFWQXPYIXQWRWSWTXAWAXBXCXDXEXG $.
      $( [23-Sep-2004] $)
  $}

  ${
    $d x y z w v f g A $.  $d x y z w v f g B $.  $d x y z w v f g C $.
    mapxpen.1 $e |- A e. _V $.
    mapxpen.2 $e |- B e. _V $.
    mapxpen.3 $e |- C e. _V $.
    $( Equinumerosity law for double set exponentiation.  Proposition 10.45 of
       [TakeutiZaring] p. 96. $)
    mapxpen $p |- ( ( A ^m B ) ^m C ) ~~ ( A ^m ( B X. C ) ) $=
      ( vx vy vz vw vv vf vg cv wcel wa wceq cvv wfn ax-17 cmap co copab2 copab
      cxp cfv oprex eqid oprabex2 a1i opabex2 wral fnoprab2 fneq1 mpbiri adantl
      fvex hboprab1 hbeleq hboprab2 wf elmap ffvelrn sylanb sylan an32s adantlr
      anasss wb opreq oprabval4g mp3an3 sylan9eq eleq1d adantll mpbird r19.21ad
      hban exp32 r19.21ai xpex ffnoprv bitri sylanbrc fopabfv simplbi adantr wi
      sylbi anassrs eqeq2d pm5.32da opabbid syl5ibr mpd eqtrd jca hbopab1 fveq1
      hbopab fvopab2 mpan2 fveq1d oprabbid fnoprv syl6bbr anbi1d ancomd fnopab2
      biimparc biantrurd ffnfv bitr2i syl6bb anbi2d syl5bb bitr4d bitr3d ralcom
      ralbida syl impbii en2 ) GHABUAUBZCUAUBZABCUEZUAUBZINZBOZJNZCOZPZKNZYHYJG
      NZUFZUFZQZPZIJKUCZYKLNZYIMNZYHYJHNZUBZQZPZIMUDZQZPZJLUDZYDCUAUGYSROYNYEOZ
      IJKBCYPYSEFYSUHZUIUJUUIROUUBYGOZJLCUUFFUKUJUUJUUBYSQZPZUULYNUUIQZPZUUNUUL
      UUOUUNUUBYFSZUUCAOZJCULZIBULZUULUUMUUQUUJUUMUUQYSYFSIJKBCYPYSYHYOUQZUUKUM
      YFUUBYSUNUOUPUUNUUSIBUUJUUMIUUJITIHYSYRIJKHURUSZVRUUNYIUURJCUUJUUMJUUJJTJ
      HYSYRIJKHUTUSVRZYIJTUUNYIYKUURUUNYLPUURYPAOZUUJYLUVDUUMUUJYIYKUVDUUJYKYIU
      VDUUJYKPYOYDOZYIUVDUUJCYDYNVAZYKUVEYDCYNABUAUGFVBZCYDYJYNVCVDZUVEBAYOVAZY
      IUVDABYODEVBZBAYHYOVCVDVEVFVHVGUUMYLUURUVDVIUUJUUMYLPUUCYPAUUMYLUUCYHYJYS
      UBZYPYHYJUUBYSVJYIYKYPROUVKYPQUVAIJKBCYPRYSUUKVKVLVMZVNVOVPVSVQVTUULYFAUU
      BVAUUQUUTPZAYFUUBDBCEFWAVBIJBCAUUBWBWCZWDUUNYNYKYTYOQZPZJLUDZUUIUUJYNUVQQ
      ZUUMUUJUVFUVRUVGUVFUVRUVEJCULZJLCYDYNWEWFWIWGUUNUVPUUHJLUVCUUNLTUUNYKUVOU
      UGUUNYKPZYOUUFYTUVTUVEYOUUFQZUUJYKUVEUUMUVHVGUUMYKUVEUWAWHUUJUVEUWAUUMYKP
      ZYOYIUUAYPQZPZIMUDZQZUVEUVIUWFUVJUVIUWFUVDIBULZIMBAYOWEWFWIUWBUUFUWEYOUWB
      UUEUWDIMUUMYKIUVBYKITZVRUWBMTUWBYIUUDUWCUWBYIPUUCYPUUAUUMYIYKUUCYPQZUUMYI
      YKUWIUVLWJVFWKWLWMWKWNVOWOWKWLWMWPWQUUPUUNUUTUUMPUUPUUMUUTUUOUUMUUTPZUULU
      UOUWJUVMUULUUOUUMUUQUUTUUOUUMUUBYLYMUUCQZPZIJKUCZQUUQUUOYSUWMUUBUUOYRUWLI
      JKIGUUIUUHJLIGYKUUGIUWHILUUFUUEIMLWRUSVRWTUSZJGUUIUUHJLGWRUSZUUOKTUUOYLYQ
      UWKUUOYLPYPUUCYMUUOYIYKYPUUCQZUUOYKYIUWPUUOYKPZYIYPYHUUFUFZUUCUWQYHYOUUFU
      UOYKYOYJUUIUFZUUFYJYNUUIWSYKUUFROUWSUUFQIMBUUCEUKZJLCUUFRXAXBVMZXCYIUUCRO
      UWRUUCQYHYJUUBUGZIMBUUCRXAXBVMZVFVHWKWLXDWKIJKBCUUBXEXFXGUVNXFXJXHUUPUUJU
      UTUUMUUOUUJUUTVIUULUUOUUJUURIBULZJCULZUUTUUOUVSUUJUXEUUOUVSYNCSZUVSPZUUJU
      UOUXFUVSUUOUXFUUICSJLCUUFUUIUWTUUIUHXICYNUUIUNUOXKUUJUVFUXGUVGJCYDYNXLXMX
      NUUOUVEUXDJCUWOUWQUVEYOBSZUXDPZUXDUVEUXHUWGPZUWQUXIUVEUVIUXJUVJIBAYOXLWCU
      WQUWGUXDUXHUWQUVDUURIBUUOYKIUWNUWHVRUWQYIPYPUUCAUXCVNXTXOXPUWQUXHUXDUWQUW
      AUXHUXAUWAUXHUUFBSIMBUUCUUFUXBUUFUHXIBYOUUFUNUOYAXKXQXTXRUURJICBXSXNUPXGV
      PYBYC $.
      $( [21-Feb-2004] $)
  $}

  ${
    $d x y z w v f g A $.  $d x y z w v f g B $.  $d x y z w v f g C $.
    xpmapen.1 $e |- A e. _V $.
    xpmapen.2 $e |- B e. _V $.
    xpmapen.3 $e |- C e. _V $.
    ${
      $d y D $.  $d y R $.  $d x S $.
      xpmapenlem.4 $e |- D =
                    { <. z , w >. | ( z e. C /\ w = U. dom { ( x ` z ) } ) } $.
      xpmapenlem.5 $e |- R =
                    { <. z , w >. | ( z e. C /\ w = U. ran { ( x ` z ) } ) } $.
      xpmapenlem.6 $e |- S = { <. z , w >. | ( z e. C /\ w =
                       <. ( U. dom { y } ` z ) , ( U. ran { y } ` z ) >. ) } $.
      $( Lemma for ~ xpmapen . $)
      xpmapenlem1 $p |- ( ( y = <. D , R >. -> A. z y = <. D , R >. ) /\
                           ( y = <. D , R >. -> A. w y = <. D , R >. ) ) $=
        ( cv wceq wal hbxfreq cop wi wcel cfv csn cdm cuni wa copab hbopab1 crn
        hbop hbeleq hbopab2 pm3.2i ) BQHIUAZRZUQCSUBUQUQDSUBCBUPCBHICBHCQZGUCZD
        QZURAQUDUEZUFUGRUHZCDUIZNVBCDBUJTCBIUSUTVAUKUGRUHZCDUIZOVDCDBUJTULUMDBU
        PDBHIDBHVCNVBCDBUNTDBIVEOVDCDBUNTULUMUO $.
        $( [2-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem2 $p |- ( ( y = <. D , R >. /\ z e. C ) ->
             ( ( U. dom { y } ` z ) = U. dom { ( x ` z ) } /\
               ( U. ran { y } ` z ) = U. ran { ( x ` z ) } ) ) $=
        ( wceq csn cuni cfv cv cop wcel cdm crn copab opeq1i sneqi syl6eq dmeqd
        wa sneq unieqd opabex2 op1sta fveq1d cvv snex dmex uniex mpan2 sylan9eq
        fvopab2 opeq2i rneqd fopabex2 op2nda rnex jca ) BUAZHIUBZQZCUAZGUCZUKVM
        VJRZUDZSZTZVMAUATZRZUDZSZQVMVOUEZSZTZVTUEZSZQVLVNVRVMVNDUAZWBQUKCDUFZTZ
        WBVLVMVQWIVLVQWIIUBZRZUDZSWIVLVPWMVLVOWLVLVOVKRZWLVJVKULZVKWKHWIINUGUHU
        IUJUMWIICDGWBMUNUOUIUPVNWBUQUCWJWBQWAVTVSURZUSUTCDGWBUQVCVAVBVLVNWEVMVN
        WHWGQUKCDUFZTZWGVLVMWDWQVLWDHWQUBZRZUEZSWQVLWCXAVLVOWTVLVOWNWTWOVKWSIWQ
        HOVDUHUIVEUMHWQCDGWBHMNVFCDGWGMUNVGUIUPVNWGUQUCWRWGQWFVTWPVHUTCDGWGUQVC
        VAVBVI $.
        $( [1-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem3 $p |- ( ( x : C --> ( A X. B ) /\ y = <. D , R >. ) ->
                        x = S ) $=
        ( cv wceq wa wcel cxp cop cfv copab wfn ffn dffn5 sylib adantr csn cuni
        wf cdm crn ax-17 wal wi xpmapenlem1 simpli simpri ffvelrn elxp4 simplbi
        hban adantlr xpmapenlem2 opeq12 adantll eqtr4d pm5.32da opabbid syl6eqr
        syl eqeq2d eqtrd ) GEFUAZAQZULZBQZHIUBRZSZVQCQZGTZDQZWBVQUCZRZSZCDUDZJV
        RVQWHRZVTVRVQGUEWIGVPVQUFCDGVQUGUHUIWAWHWCWDWBVSUJZUMUKUCZWBWJUNUKUCZUB
        ZRZSZCDUDJWAWGWOCDVRVTCVRCUOVTVTCUPUQZVTVTDUPUQZABCDEFGHIJKLMNOPURZUSVD
        VRVTDVRDUOWPWQWRUTVDWAWCWFWNWAWCSZWEWMWDWSWEWEUJZUMUKZWTUNUKZUBZWMVRWCW
        EXCRZVTVRWCSWEVPTZXDGVPWBVQVAXEXDXAETXBFTSWEEFVBVCVMVEVTWCWMXCRZVRVTWCS
        WKXARWLXBRSXFABCDEEGHIJKKMNOPVFWKWLXAXBVGVMVHVIVNVJVKPVLVO $.
        $( [1-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem4 $p |- ( ( ( y = <. U. dom { y } , U. ran { y } >. /\
        ( U. dom { y } : C --> A /\ U. ran { y } : C --> B ) ) /\ x = S ) ->
                     ( x : C --> ( A X. B ) /\ y = <. D , R >. ) ) $=
        ( cuni wceq wa wcel csn cdm crn cop cxp wral ffvelrn r19.21aiva anim12i
        cv wf copab wb eqtr feq1d mpan2 fvex opelxp ralbii eqid fopab2 3bitr3ri
        cfv r19.26 biimpac sylan adantll fopabfv simplbi hbopab1 hbxfreq hbeleq
        syl6rbbr hbopab2 fveq1d opex fvopab2 sylan9eq sneqd dmeqd unieqd op1sta
        cvv syl6eq eqeq2d pm5.32da opabbid syl5req adantlr rneqd op2nda opeq12d
        eqeq1 syl5ibr exp3a imp31 jca ) BUJZWRUAZUBQZWSUCQZUDZRZGEWTUKZGFXAUKZS
        ZSAUJZJRZSGEFUEZXGUKZWRHIUDZRZXFXHXJXCXFCUJZWTVCZETZCGUFZXMXAVCZFTZCGUF
        ZSZXHXJXDXPXEXSXDXOCGGEXMWTUGUHXEXRCGGFXMXAUGUHUIXHXTXJXHXJGXIXMGTZDUJZ
        XNXQUDZRSZCDULZUKZXTXHJYERZXJYFUMPXHYGSGXIXGYEXGJYEUNZUOUPYCXITZCGUFXOX
        RSZCGUFYFXTYIYJCGXNXQEFXMXAUQZURUSCDGXIYCYEYEUTVAXOXRCGVDVBVMVEVFVGXCXF
        XHXLXCXFXHXLXFXHSZXLXCXBXKRYLWTHXAIXDXHWTHRXEXDXHWTYAYBXNRZSZCDULZHXDWT
        YORXPCDGEWTVHVIXHHYAYBXMXGVCZUAZUBZQZRZSZCDULYONXHUUAYNCDCAJCAJYEPYDCDA
        VJVKVLZDAJDAJYEPYDCDAVNVKVLZXHYAYTYMXHYASZYSXNYBUUDYSYCUAZUBZQXNUUDYRUU
        FUUDYQUUEUUDYPYCXHYAYPXMYEVCZYCXHXMXGYEXHYGXGYERPYHUPVOYAYCWCTUUGYCRXNX
        QVPCDGYCWCVQUPVRVSZVTWAXNXQXMWTUQZWBWDWEWFWGWHVRWIXEXHXAIRXDXEXHXAYAYBX
        QRZSZCDULZIXEXAUULRXSCDGFXAVHVIXHIYAYBYQUCZQZRZSZCDULUULOXHUUPUUKCDUUBU
        UCXHYAUUOUUJUUDUUNXQYBUUDUUNUUEUCZQXQUUDUUMUUQUUDYQUUEUUHWJWAXNXQUUIYKW
        KWDWEWFWGWHVRVGWLWRXBXKWMWNWOWPWQ $.
        $( [2-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem5 $p |- ( ( A X. B ) ^m C ) ~~ ( ( A ^m C ) X. ( B ^m C ) ) $=
        ( cvv wcel wceq wa cxp cmap co cop oprex opex a1i csn cdm cuni fopabex2
        cv cfv crn opelxp mpbir2an eleq1 mpbiri adantl elxp4 simplbi copab wral
        wf syl op1sta dmeqd unieqd wal wi xpmapenlem1 simpli simpri xpmapenlem2
        sneq simpld eqeq2d pm5.32da opabbid syl6eqr 3eqtr4a op2nda rneqd simprd
        ffvelrn r19.21aiva adantr ax-17 xpmapenlem3 fveq1d fveq1i fvopab2 mpan2
        jca hban syl5eq sylan9eq eleq1d syl6bb ralbida r19.26 mpbid an4 fopabfv
        anbi12i bitr4i sylib jca31 xpmapenlem4 impbii elmap anbi1i anbi2i bitri
        fvex xpex 3bitr4i en2 ) ABEFUAZGUBUCZEGUBUCZFGUBUCZUAZHIUDZJXSGUBUEYDQR
        AULZXTRZHIUFUGJQRBULZYCRZCDGCULZYGUHZUIZUJZUMZYIYJUNZUJZUMZUDZJMPUKUGGX
        SYEVDZYGYDSZTZYGYLYOUDSZGEYLVDZGFYOVDZTZTZYEJSZTZYFYSTYHUUFTYTUUGYTUUAU
        UDUUFYTYGQQUAZRZUUAYSUUIYRYSUUIYDUUHRZUUJHQRIQRHIQQCDGYIYEUMZUHZUNUJZIM
        OUKZUOCDGUULUIUJZHMNUKZUUNUPYGYDUUHUQURUSUUIUUAYLQRYOQRTYGQQUTVAVEYTYLY
        IGRZDULZYMSZTZCDVBZSZYOUUQUURYPSZTZCDVBZSZTZYMERZCGVCZYPFRZCGVCZTZTZUUD
        YTUVGUVLYSUVGYRYSUVBUVFYSYDUHZUIZUJHYLUVAHIUUPVFYSYKUVOYSYJUVNYGYDVOZVG
        VHYSUVAUUQUURUUOSZTZCDVBHYSUUTUVRCDYSYSCVIVJZYSYSDVIVJZABCDEFGHIJKLMNOP
        VKZVLZUVSUVTUWAVMZYSUUQUUSUVQYSUUQTZYMUUOUURUWDYMUUOSZYPUUMSZABCDEFGHIJ
        KLMNOPVNZVPVQVRVSNVTWAYSUVNUNZUJIYOUVEHIUUPUUNWBYSYNUWHYSYJUVNUVPWCVHYS
        UVEUUQUURUUMSZTZCDVBIYSUVDUWJCDUWBUWCYSUUQUVCUWIUWDYPUUMUURUWDUWEUWFUWG
        WDVQVRVSOVTWAWNUSYTUUKXSRZCGVCZUVLYRUWLYSYRUWKCGGXSYIYEWEWFWGYTUWLUVHUV
        JTZCGVCUVLYTUWKUWMCGYRYSCYRCWHUWBWOYTUUQTZUWKYQXSRUWMUWNUUKYQXSYTUUQUUK
        YIJUMZYQYTYIYEJABCDEFGHIJKLMNOPWIZWJUUQUWOYIUUQUURYQSTCDVBZUMZYQYIJUWQP
        WKUUQYQQRUWRYQSYMYPUFCDGYQQWLWMWPWQWRYMYPEFYIYOXOUOWSWTUVHUVJCGXAWSXBWN
        UVMUVBUVITZUVFUVKTZTUUDUVBUVFUVIUVKXCUUBUWSUUCUWTCDGEYLXDCDGFYOXDXEXFXG
        UWPXHABCDEFGHIJKLMNOPXIXJYFYRYSXSGYEEFKLXPMXKXLYHUUEUUFYHUUAYLYARZYOYBR
        ZTZTUUEYGYAYBUTUXCUUDUUAUXAUUBUXBUUCEGYLKMXKFGYOLMXKXEXMXNXLXQXR $.
        $( [1-May-2004] $)
    $}

    $( Equinumerosity law for set exponentiation of a cross product.  Exercise
       4.47 of [Mendelson] p. 255. $)
    xpmapen $p |- ( ( A X. B ) ^m C ) ~~ ( ( A ^m C ) X. ( B ^m C ) ) $=
      ( vx vy vz vw cv cfv csn cdm cuni wceq wa copab crn eqid wcel xpmapenlem5
      cop ) GHIJABCIKZCUAZJKZUDGKLMZNOPQIJRZUEUFUGSOPQIJRZUEUFUDHKMZNOLUDUJSOLU
      CPQIJRZDEFUHTUITUKTUB $.
      $( [23-Feb-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    mapunen.1 $e |- A e. _V $.
    mapunen.2 $e |- B e. _V $.
    mapunen.3 $e |- C e. _V $.
    $( Equinumerosity law for set exponentiation of a disjoint union.  Exercise
       4.45 of [Mendelson] p. 255. $)
    mapunen $p |- ( ( A i^i B ) = (/) ->
                  ( C ^m ( A u. B ) ) ~~ ( ( C ^m A ) X. ( C ^m B ) ) ) $=
      ( c0 wceq cun cmap cres cint cvv wcel wi wf wa elmap sylbi syl6eq cin cxp
      vx vy co cv cop csn crn oprex a1i wss ssun1 fssres mpan2 sylibr ssun2 jca
      cuni unex visset resexg ax-mp opelxp 3imtr4i elxp5 simprbi anbi12i fun wb
      sylib unidm feq3 bitr4i expcom wfn anim12i reseq1 resundir fnresdm uneq1d
      sylan ffn incom eqeq1i fnresdisj biimpa sylan2b uneq2d sylan9eq sylan9eqr
      un0 anassrs uncom eqtri an32s opeq12d eqeq2d biimparc exp44 sylan2 impcom
      imp adantrl inteq inteqd op1stb sneq rneqd unieqd op2nda uneq12d syl6reqr
      syl resundi sylan9req ex ad2antrl impbid en3d ) ABUAZGHZUCUDCABIZJUEZCAJU
      EZCBJUEZUBZUCUFZAKZYHBKZUGZUDUFZLZLZYLUHZUIZUSZIZYDMNYBCYCJUJUKYHYDNZYKYG
      NZOYBYCCYHPZYIYENZYJYFNZQYSYTUUAUUBUUCUUAACYIPZUUBUUAAYCULUUDABUMYCCAYHUN
      UOCAYIFDRUPUUABCYJPZUUCUUABYCULUUEBAUQYCCBYHUNUOCBYJFERUPURCYCYHFABDEUTZR
      ZYIYJYEYFYHMNZYJMNUCVAZYHBMVBVCZVDVEUKYLYGNZYBYRYDNZUUKACYNPZBCYQPZQZYBUU
      LUUKYNYENZYQYFNZQZUUOUUKYLYNYQUGZHZUURYLYEYFVFZVGUUPUUMUUQUUNCAYNFDRZCBYQ
      FERZVHVKUUOYBQYCCCIZYRPZUULABCCYNYQVIUULYCCYRPZUVECYCYRFUUFRUVDCHUVEUVFVJ
      CVLUVDCYCYRVMVCVNUPWBVOYBYSUUKQZYHYRHZYLYKHZVJYBUVGQUVHUVIYBUUKUVHUVIOZYS
      UUKYBUVJUUKUUTUURQYBUVJOZUVAUURUUTYNAVPZYQBVPZQZUVKUUPUVLUUQUVMUUPUUMUVLU
      VBACYNWCSUUQUUNUVMUVCBCYQWCSVQUUTUVNUVKUUTUVNYBUVHUVIUVNYBQZUVHQZUVIUUTUV
      PYKUUSYLUVPYIYNYJYQUVHUVOYIYNAKZYQAKZIZYNUVHYIYRAKUVSYHYRAVRYNYQAVSTUVLUV
      MYBUVSYNHUVLUVMYBQZUVSYNUVRIZYNUVLUVQYNUVRAYNVTWAUVTUWAYNGIYNUVTUVRGYNYBU
      VMBAUAZGHZUVRGHZYAUWBGABWDWEUVMUWCUWDBAYQWFWGWHWIYNWLTWJWMWKUVHUVOYJYNBKZ
      YQBKZIZYQUVHYJYRBKUWGYHYRBVRYNYQBVSTUVLYBUVMUWGYQHUVMUVLYBQZUWGUWEYQIZYQU
      VMUWFYQUWEBYQVTWIUWHUWIGYQIZYQUWHUWEGYQUVLYBUWEGHABYNWFWGWAUWJYQGIYQGYQWN
      YQWLWOTWKWPWKWQWRWSWTXCXASXBXDYSUVIUVHOYBUUKYSUVIUVHYSUVIYHYHYCKZYRYSYHYC
      VPZUWKYHHYSUUAUWLUUGYCCYHWCSYCYHVTXNUVIYRYIYJIUWKUVIYNYIYQYJUVIYNYKLZLYIU
      VIYMUWMYLYKXEXFYIYJUUHYIMNUUIYHAMVBVCZXGTUVIYQYKUHZUIZUSYJUVIYPUWPUVIYOUW
      OYLYKXHXIXJYIYJUWNUUJXKTXLYHABXOXMXPXQXRXSXQXT $.
      $( [23-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d x B $.
    pwen.1 $e |- B e. _V $.
    $( If two sets are equinumerous, then their power sets are equinumerous.
       Proposition 10.15 of [TakeutiZaring] p. 87. $)
    pwen $p |- ( A ~~ B -> ~P A ~~ ~P B ) $=
      ( vx cvv wcel cen wbr cpw relen wi c2o cmap co con0 2on oprex pw2en mp2an
      wb brrelexi cv wceq breq1 pweq breq1d imbi12d enrefg ax-mp elisseti mapen
      mpan visset enen1 enen2 bitr2i sylib vtoclg mpcom ) AEFABGHZAIZBIZGHZABGJ
      UADUBZBGHZVDIZVBGHZKUTVCKDAEVDAUCZVEUTVGVCVDABGUDVHVFVAVBGVDAUEUFUGVELVDM
      NZLBMNZGHZVGLLGHZVEVKLOFVLPLOUHUILLVDBLOPUJCUKULVGVIVBGHZVKVIEFVFVIGHVGVM
      TLVDMQVDDUMRVFVIVBEUNSVJEFVBVJGHVMVKTLBMQBCRVBVJVIEUOSUPUQURUS $.
      $( [29-Mar-2007] $) $( [29-Jan-2004] $)
  $}

  ${
    $d x y z f A $.  $d x y z f B $.  $d x y z f C $.
    ssenen.1 $e |- A e. _V $.
    ssenen.2 $e |- B e. _V $.
    $( Equinumerosity of equinumerous subsets of a set. $)
    ssenen $p |- ( A ~~ B ->
             { x | ( x C_ A /\ x ~~ C ) } ~~ { x | ( x C_ B /\ x ~~ C ) } ) $=
      ( vf cen wbr cv cab cin wss wa cima cvv wcel sylan syl wceq vy vz cpw wex
      wf1o bren ccnv pwex inex1 a1i wf1 f1of1 visset f1imaen entr wfo f1ofo crn
      expl imassrn forn sseq2d mpbii jctild elin elpw breq1 elab anbi12i imaexg
      bitri ax-mp 3imtr4g wi f1ocnv cnvex simpl sylib imaeq2 wrel f1orel dfrel2
      wb imaeq1d adantr f1imacnv eqtr3d sylan9eqr eqcomd sylan2 adantrl adantrr
      sylbi ex impbid en3d 19.23aiv df-pw ineq1i inab eqtri 3brtr3g ) BCHIZBUCZ
      AJZDHIZAKZLZCUCZXGLZXEBMZXFNAKZXECMZXFNAKZHXCBCGJZUEZGUDXHXJHIZBCGFUFXPXQ
      GXPUAUBXHXJXOUAJZOZXOUGZUBJZOZXHPQXPXDXGBEUHUIUJXPXRBMZXRDHIZNZXSCMZXSDHI
      ZNZXRXHQZXSXJQZXPYEYGYFXPYCYDYGXPYCNZXSXRHIZYDYGXPBCXOUKZYCYLBCXOULZBCXRX
      OUAUMZUNRXSXRDUORUSXPBCXOUPZYFBCXOUQYPXSXOURZMYFXOXRUTYPYQCXSBCXOVAVBVCSV
      DYIXRXDQZXRXGQZNZYEXRXDXGVEZYRYCYSYDXRBYOVFZXFYDAXRYOXEXRDHVGVHVIVKYJXSXI
      QZXSXGQZNYHXSXIXGVEUUCYFUUDYGXSCXOPQXSPQGUMZXOXRPVJVLZVFXFYGAXSUUFXEXSDHV
      GVHVIVKVMXPYACMZYADHIZNZYBBMZYBDHIZNZYAXJQZYBXHQZXPCBXTUEZUUIUULVNBCXOVOZ
      UUOUUIUUKUUJUUOUUGUUHUUKUUOUUGNYBYAHIZUUHUUKUUOCBXTUKZUUGUUQCBXTULZCBYAXT
      UBUMZUNRYBYADUORUSUUOCBXTUPZUUJCBXTUQUVAYBXTURZMUUJXTYAUTUVAUVBBYBCBXTVAV
      BVCSVDSUUMYAXIQZYAXGQZNZUUIYAXIXGVEZUVCUUGUVDUUHYACUUTVFZXFUUHAYAUUTXEYAD
      HVGVHVIVKUUNYBXDQZYBXGQZNUULYBXDXGVEUVHUUJUVIUUKYBBXTPQYBPQXOUUEVPXTYAPVJ
      VLZVFXFUUKAYBUVJXEYBDHVGVHVIVKVMXPYIUUMNZXRYBTZYAXSTZWCXPUVKNUVLUVMXPUUMU
      VLUVMVNZYIUUMXPUUGUVNUUMUVEUUGUVFUVEUVCUUGUVCUVDVQUVGVRWMXPUUGNZUVLUVMUVO
      UVLNXSYAUVLUVOXSXOYBOZYAXRYBXOVSUVOXTUGZYBOZUVPYAXPUVRUVPTUUGXPUVQXOYBXPX
      OVTUVQXOTBCXOWAXOWBVRWDWEXPUURUUGUVRYATXPUUOUURUUPUUSSCBYAXTWFRWGWHWIWNWJ
      WKXPYIUVMUVLVNZUUMYIXPYCUVSYIYTYCUUAYTYRYCYRYSVQUUBVRWMYKUVMUVLYKUVMNYBXR
      UVMYKYBXTXSOZXRYAXSXTVSXPYMYCUVTXRTYNBCXRXOWFRWHWIWNWJWLWOWNWPWQWMXHXKAKZ
      XGLXLXDUWAXGABWRWSXKXFAWTXAXJXMAKZXGLXNXIUWBXGACWRWSXMXFAWTXAXB $.
      $( [21-Mar-2007] $) $( [30-Sep-2004] $)
  $}

  ${
    $d x y A $.
    limenpsi.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to a proper subset of itself. $)
    limenpsi $p |- ( A e. V -> A ~~ ( A \ { (/) } ) ) $=
      ( vx vy wcel c0 csn cdif cdom wbr cv csuc wb ax-mp con0 wceq ordelon mpan
      cen wne wlim limsuc biimpi nsuceq0 jctir eldifsn sylibr word limord suc11
      wa syl2an dom2 wss difss ssdom2g mpi sbth syl11anc ) ABFZAAGHZIZJKVCAJKZA
      VCTKDEAVCDLZMZELZMZBVEAFZVFAFZVFGUAZULVFVCFVIVJVKVIVJAUBZVIVJNCAVEUCOUDVE
      UEUFVFAGUGUHVIVEPFZVGPFZVFVHQVEVGQNVGAFZAUIZVIVMVLVPCAUJOZAVERSVPVOVNVQAV
      GRSVEVGUKUMUNVAVCAUOVDAVBUPVCABUQURAVCUSUT $.
      $( [30-Oct-2003] $)
  $}

  ${
    limensuci.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to its successor. $)
    limensuci $p |- ( A e. V -> A ~~ suc A ) $=
      ( wcel csn cdif cun csuc cen wbr cvv difexg limenpsi ensymg 0ex cin ax-mp
      c0 wceq mpbi sylc en2sn mpan wa incom disjdif eqtri wn word limord ordirr
      wlim disjsn mpbir unen syl11anc wss 0ellim snss undif uncom eqtr3i df-suc
      mpanr12 3brtr4g ) ABDZAREZFZVGGZAAEZGZAAHIVFVHAIJZVGVJIJZVIVKIJZVFVHKDAVH
      IJVLAVGBLABCMAVHKNUARKDVFVMORAKBUBUCVLVMUDVHVGPZRSAVJPRSZVNVOVGVHPRVHVGUE
      VGAUFUGVPAADUHZAUIZVQAULZVRCAUJQAUKQAAUMUNVHAVGVJUOVDUPVGVHGZAVIVGAUQZVTA
      SRADZWAVSWBCAURQRAOUSTVGAUTTVGVHVAVBAVCVE $.
      $( [30-Oct-2003] $)
  $}

  $( A limit ordinal is equinumerous to its successor. $)
  limensuc $p |- ( ( A e. V /\ Lim A ) -> A ~~ suc A ) $=
    ( wlim wcel csuc cen wbr wi con0 cif wceq eleq1 suceq breq12d imbi12d limeq
    id limon elimhyp limensuci dedth impcom ) ACZABDZAAEZFGZUCUDUFHUCAIJZBDZUGU
    GEZFGZHAIAUGKZUDUHUFUJAUGBLUKAUGUEUIFUKQAUGMNOUGBUCUGCICAIAUGPIUGPRSTUAUB
    $.
    $( [30-Oct-2003] $)

  ${
    $d x y A $.
    $( Any infinite ordinal is equinumerous to its successor.  Exercise 7 of
       [TakeutiZaring] p. 88.  Proved without the Axiom of Infinity.  (Revised
       by Mario Carneiro, 13-Jan-2013.) $)
    infensuc $p |- ( ( A e. On /\ om C_ A ) -> A ~~ suc A ) $=
      ( vx vy con0 wcel com wss wa csuc cen wbr wceq cvv wi cv id suceq breq12d
      csn visset onprc eleq1 mtbiri ssexg ancoms nsyl3 omon ori nsyl2 weq limom
      limensuci sucex en2sn mp2an cin c0 wel wn word eloni ordirr disjsn sylibr
      syl sucelon 3imtr4i jca cun unen df-suc 3brtr4g syl5 mpan2 com12 ad2antrr
      ex wlim wral limensuc mpan a1d tfindsg exp31 com23 imp mpd ) ADEZFAGZHZFD
      EZAAIZJKZWJFDLZWKWNFMEZWJWNWODMEUAFDMUBUCWIWHWOFADUDUEUFWKWNUGUHUIWHWIWKW
      MNWHWKWIWMWHWKWIWMBOZWPIZJKZFFIZJKCOZWTIZJKZXAXAIZJKZWMBCAFWPFLZWPFWQWSJX
      EPWPFQRBCUJZWPWTWQXAJXFPWPWTQRWPXALZWPXAWQXCJXGPWPXAQRWPALZWPAWQWLJXHPWPA
      QRFDUKULWTDEZXBXDNWKFWTGZXBXIXDXBWTSZXASZJKZXIXDNWTMEXAMEXMCTZWTXNUMWTXAM
      MUNUOXIWTXKUPUQLZXAXLUPUQLZHZXBXMHZXDXIXOXPXICCURUSZXOXIWTUTXSWTVAWTVBVEW
      TWTVCVDXADEZXAXAEUSZXIXPXTXAUTYAXAVAXAVBVEWTVFXAXAVCVGVHXRXQXDXRXQHWTXKVI
      XAXLVIXAXCJWTXAXKXLVJWTVKXAVKVLVQVMVNVOVPWPVRZWKHFWPGZHWRXJXBNCWPVSYBWRWK
      YCWPMEYBWRBTWPMVTWAVPWBWCWDWEWFWG $.
      $( [13-Jan-2013] $) $( [30-Oct-2003] $)
  $}

