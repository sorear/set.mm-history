$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equivalence relations and classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Er $.  $( Equivalence predicate $)
  $c /. $.  $( Long slash for quotient set $)

  $( Extend the definition of a wff to include the equivalence predicate. $)
  wer $a wff Er R $.

  $( Extend the definition of a class to include equivalence class. $)
  cec $a class [ A ] R $.

  $( Extend the definition of a class to include quotient set. $)
  cqs $a class ( A /. R ) $.

  $( Define the equivalence predicate. ` R ` need not be a relation but
     ordinarily will be, in which case we call it an equivalence relation.  Our
     notation is not standard.  A formal notation doesn't seem to exist in the
     literature; instead only informal English tends to be used.  Some
     definitions in the literature may also require that ` R ` be a relation.
     The present definition, although somewhat cryptic, nicely avoids dummy
     variables.  In ~ dfer2 we derive a more typical definition.  We show that
     an equivalence relation is reflexive, symmetric, and transitive in
     ~ erref , ~ ersymb , and ~ ertr . $)
  df-er $a |- ( Er R <-> ( `' R u. ( R o. R ) ) C_ R ) $.

  ${
    $d x y z R $.
    $( Alternate definition of equivalence predicate. $)
    dfer2 $p |- ( Er R <-> A. x A. y A. z
              ( ( x R y -> y R x ) /\ ( ( x R y /\ y R z ) -> x R z ) ) ) $=
      ( wer ccnv ccom cun wss cv wbr wi wal df-er cnvsym cotr albii 19.26 bitri
      wa anbi12i unss 19.28v bitr2i 3bitr3i ) DEDFZDDGZHDIZAJZBJZDKZUJUIDKLZUKU
      JCJZDKTUIUMDKLZTCMZBMZAMZDNUFDIZUGDIZTULBMZAMZUNCMZBMZAMZTZUHUQURVAUSVDAB
      DOABCDPUAUFUGDUBUQUTVCTZAMVEUPVFAUPULVBTZBMVFUOVGBULUNCUCQULVBBRSQUTVCARU
      DUES $.
      $( [3-Jan-1997] $)
  $}

  $( Define the ` R ` -coset of ` A ` .  Exercise 35 of [Enderton] p. 61.  This
     is called the equivalence class of ` A ` modulo ` R ` when ` R ` is an
     equivalence relation (i.e. when ` Er R ` ; see ~ dfer2 ).  In this case,
     ` A ` is a representative (member) of the equivalence class ` [ A ] R ` ,
     which contains all sets that are equivalent to ` A ` .  Definition of
     [Enderton] p. 57 uses the notation ` [ A ] ` (subscript) ` R ` , although
     we simply follow the brackets by ` R ` since we don't have subscripted
     expressions.  For an alternate definition, see ~ dfec2 . $)
  df-ec $a |- [ A ] R = ( R " { A } ) $.

  ${
    $d y A x $.  $d y R x $.
    ec2.1 $e |- A e. _V $.
    $( Alternate definition of ` R ` -coset of ` A ` .  Definition 34 of
       [Suppes] p. 81. $)
    dfec2 $p |- [ A ] R = { y | A R y } $=
      ( vx csn cima cv wcel cop wex cab cec wbr dfima3 df-ec wceq opeq1 eleq1d
      wa ceqsexv elsn anbi1i exbii df-br 3bitr4ri abbii 3eqtr4i ) CBFZGEHZUIIZU
      JAHZJZCIZTZEKZALBCMBULCNZALEACUIOBCPUQUPAUJBQZUNTZEKBULJZCIZUPUQUNVAEBDUR
      UMUTCUJBULRSUAUOUSEUKURUNEBUBUCUDBULCUEUFUGUH $.
      $( [3-Jan-1997] $)
  $}

  $( An equivalence class modulo a set is a set. $)
  ecexg $p |- ( R e. B -> [ A ] R e. _V ) $=
    ( wcel cec csn cima cvv df-ec imaexg syl5eqel ) CBDACECAFZGHACICLBJK $.
    $( [24-Jul-1995] $)

  ${
    $d x y A $.  $d x y R $.
    $( Define quotient set. ` R ` is usually an equivalence relation.
       Definition of [Enderton] p. 58. $)
    df-qs $a |- ( A /. R ) = { y | E. x e. A y = [ x ] R } $.
  $}

  $( Equality theorem for equivalence predicate. $)
  ereq $p |- ( R = S -> ( Er R <-> Er S ) ) $=
    ( wceq ccnv ccom cun wss cnveq coeq1 coeq2 eqtrd uneq12d sseq1d sseq2 bitrd
    wer df-er 3bitr4g ) ABCZADZAAEZFZAGZBDZBBEZFZBGZAPBPSUCUFAGUGSUBUFASTUDUAUE
    ABHSUABAEUEABAIABBJKLMABUFNOAQBQR $.
    $( [4-Jun-1995] $)

  ${
    $d x y z R $.
    ster.1 $e |- ( x R y -> y R x ) $.
    ster.2 $e |- ( ( x R y /\ y R z ) -> x R z ) $.
    $( A symmetric, transitive relation is an equivalence relation. $)
    ster $p |- Er R $=
      ( wer cv wbr wi wa wal dfer2 pm3.2i gen2 mpgbir ) DGAHZBHZDIZRQDIJZSRCHZD
      IKQUADIJZKZCLBLAABCDMUCBCTUBEFNOP $.
      $( [4-Jun-1995] $)
  $}

  ${
    $d x y z $.
    $( The identity relation is an equivalence relation.  (The proof was
       shortened by Andrew Salmon, 22-Oct-2011.) $)
    ider $p |- Er _I $=
      ( cid wer ccnv ccom cun wss cnvi eqimssi wrel wceq reli ax-mp unssi df-er
      coi1 mpbir ) ABACZAADZEAFQRAQAGHRAAIRAJKAOLHMANP $.
      $( [22-Oct-2011] $) $( [10-May-1998] $)
  $}

  ${
    $d y A $.  $d v x B $.  $d v w z R $.  $d v w x y z $.
    eqer.1 $e |- ( x = y -> A = B ) $.
    eqer.2 $e |- R = { <. x , y >. | A = B } $.
    $( Lemma for ~ eqer . $)
    eqerlem $p |- ( z R w <-> [_ z / x ]_ A = [_ w / x ]_ A ) $=
      ( vv cv wceq wsbc csb cvv wcel wb vex ax-mp eqeq2i wbr brabsb ax-17 sbbii
      sbceq1g csbief bitr4i sbceq2g csbcog bitri 3bitri ) CKZDKZGUAEFLZAULMZBUM
      MAULENZABKZENZLZBUMMZUPAUMENZLZUNABCDGIUBUOUSBDUOUPFLZUSULOPUOVCQCRAULEFO
      UESURFUPAJUQEFBRJKFPAUCHUFTUGUDUTUPBUMURNZLZVBUMOPZUTVEQDRZBUMUPUROUHSVDV
      AUPVFVDVALVGABUMEOUISTUJUK $.
      $( [17-Mar-2008] $)

    $( Equivalence relation involving equality of dependent classes ` A ( x ) `
       and ` B ( y ) ` . $)
    eqer $p |- Er R $=
      ( vz vw vv cv csb wceq wbr id eqcomd eqerlem 3imtr4i wa eqtr anbi12i ster
      ) HIJEAHKZCLZAIKZCLZMZUFUDMUCUEENZUEUCENUGUDUFUGOPABHICDEFGQZABIHCDEFGQRU
      GUFAJKZCLZMZSUDUKMUHUEUJENZSUCUJENUDUFUKTUHUGUMULUIABIJCDEFGQUAABHJCDEFGQ
      RUB $.
      $( [17-Mar-2008] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    ersym.1 $e |- A e. _V $.
    ersym.2 $e |- B e. _V $.
    ersym.3 $e |- Er R $.
    $( An equivalence relation is symmetric. $)
    ersym $p |- ( A R B -> B R A ) $=
      ( vx vy vz cv wbr wi wceq wa breq12 wb ancoms imbi12d wal a4i mpbi simpli
      wer dfer2 vtocl2 ) GJZHJZCKZUGUFCKZLZABCKZBACKZLGHABDEUFAMZUGBMZNUHUKUIUL
      UFAUGBCOUNUMUIULPUGBUFACOQRUJUHUGIJZCKNUFUOCKLZUJUPNZIUQISZHURHSZGCUCUSGS
      FGHICUDUATTTUBUE $.
      $( [4-Jun-1995] $)
  $}

  ${
    ersymb.1 $e |- A e. _V $.
    ersymb.2 $e |- B e. _V $.
    ersymb.3 $e |- Er R $.
    $( An equivalence relation is symmetric. $)
    ersymb $p |- ( A R B <-> B R A ) $=
      ( wbr ersym impbii ) ABCGBACGABCDEFHBACEDFHI $.
      $( [30-Jul-1995] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d R x y z $.
    errsym.1 $e |- B e. _V $.
    errsym.2 $e |- Er R $.
    errsym.3 $e |- Rel R $.
    $( An equivalence relation is symmetric.  This version of ~ ersym avoids
       some set existence assumptions when we can be assured that ` R ` is a
       relation.  (Contributed by Mario Carneiro, 6-May-2013.) $)
    errsym $p |- ( A R B -> B R A ) $=
      ( vx vy vz cvv wbr cv wi wceq breq1 breq2 imbi12d wa wal a4i brrelexi wer
      wcel dfer2 mpbi simpli vtocl vtoclg mpcom ) AJUCABCKZBACKZABCFUAGLZBCKZBU
      LCKZMZUJUKMGAJULANUMUJUNUKULABCOULABCPQULHLZCKZUPULCKZMZUOHBDUPBNUQUMURUN
      UPBULCPUPBULCOQUSUQUPILZCKRULUTCKMZUSVARZIVBISZHVCHSZGCUBVDGSEGHICUDUETTT
      UFUGUHUI $.
      $( [6-May-2013] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    ertr.1 $e |- A e. _V $.
    ertr.2 $e |- B e. _V $.
    ertr.3 $e |- C e. _V $.
    ertr.4 $e |- Er R $.
    $( An equivalence relation is transitive. $)
    ertr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
      ( vx vy vz cv wbr wa wi wceq breq1 breq2 wal a4i imbi12d anbi12d syl3an9b
      anbi1d imbi1d anbi2d wer dfer2 mpbi simpri vtocl3 ) ILZJLZDMZUMKLZDMZNZUL
      UODMZOZABDMZBCDMZNZACDMZOZIJKABCEFGULAPZUSAUMDMZUPNZAUODMZOUMBPZUTBUODMZN
      ZVHOUOCPZVDVEUQVGURVHVEUNVFUPULAUMDQUDULAUODQUAVIVGVKVHVIVFUTUPVJUMBADRUM
      BUODQUBUEVLVKVBVHVCVLVJVAUTUOCBDRUFUOCADRUAUCUNUMULDMOZUSVMUSNZKVNKSZJVOJ
      SZIDUGVPISHIJKDUHUITTTUJUK $.
      $( [4-Jun-1995] $)
  $}

  ${
    $d A x $.  $d B x $.  $d R x y $.
    erref2.1 $e |- Er R $.
    erref2.2 $e |- dom R = B $.
    $( An equivalence relation is reflexive on its field.  Similar to
       ~ erref .  Compare Theorem 3M of [Enderton] p. 56.  (Revised by Mario
       Carneiro, 6-May-2013.) $)
    erref2 $p |- ( A e. B -> A R A ) $=
      ( vx vy cv wbr wceq wb breq12 anidms wcel cdm eleq2i wex vex eldm ersym
      ertr mpdan exlimiv sylbi sylbir vtoclga ) FHZUGCIZAACIZFABUGAJUHUIKUGAUGA
      CLMUGBNUGCOZNZUHUJBUGEPUKUGGHZCIZGQUHGUGCFRZSUMUHGUMULUGCIUHUGULCUNGRZDTU
      GULUGCUNUOUNDUAUBUCUDUEUF $.
      $( [6-May-2013] $)
  $}

  ${
    $d x y z R $.  $d x y A $.  $d y B $.  $d x y z C $.
    errtr.1 $e |- Er R $.
    errtr.2 $e |- Rel R $.
    $( An equivalence relation is transitive.  This version of ~ ertr avoids
       some set existence assumptions when we can be assured that ` R ` is a
       relation.  (Revised by Mario Carneiro, 6-May-2013.) $)
    errtr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
      ( vx vy vz cvv wcel wa wbr cv wi wceq breq1 breq2 wal a4i brrelexi anbi1d
      anim12i imbi12d anbi12d imbi1d anbi2d wer dfer2 mpbi simpri vtoclg wn cdm
      vex breldm eqid erref2 syl adantr brprc syl5ibr pm2.61i vtocl2g mpcom ) A
      JKZBJKZLABDMZBCDMZLZACDMZVHVFVIVGABDFUABCDFUAUCGNZHNZDMZVMCDMZLZVLCDMZOZA
      VMDMZVOLZVKOVJVKOGHABJJVLAPZVPVTVQVKWAVNVSVOVLAVMDQUBVLACDQUDVMBPZVTVJVKW
      BVSVHVOVIVMBADRVMBCDQUEUFCJKZVRVNVMINZDMZLZVLWDDMZOZVRICJWDCPZWFVPWGVQWIW
      EVOVNWDCVMDRUGWDCVLDRUDVNVMVLDMOZWHWJWHLZIWKISZHWLHSZGDUHWMGSEGHIDUIUJTTT
      UKULVPVQWCUMVLVLDMZVNWNVOVNVLDUNZKWNVLVMDGUOUPVLWODEWOUQURUSUTVLCDVAVBVCV
      DVE $.
      $( [4-Jun-1995] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    erref.1 $e |- Er R $.
    $( An equivalence relation is reflexive on its field.  Similar to
       ~ erref2 . $)
    erref $p |- ( A e. ( dom R u. ran R ) -> A R A ) $=
      ( vx vy cv wbr cdm crn cun wceq breq1 breq2 wex vex ersymb anidms exlimiv
      wcel sylbi bitrd wo elun eldm ertr sylan2b elrn sylanb jaoi vtoclga ) DFZ
      UKBGZAABGZDABHZBIZJZUKAKULAUKBGUMUKAUKBLUKAABMUAUKUPSUKUNSZUKUOSZUBULUKUN
      UOUCUQULURUQUKEFZBGZENULEUKBDOZUDUTULEUTULUTUTUSUKBGZULUKUSBVAEOZCPUKUSUK
      BVAVCVACUEZUFQRTURVBENULEUKBVAUGVBULEVBULVBUTVBULUSUKBVCVACPVDUHQRTUITUJ
      $.
      $( [4-Jun-1995] $)
  $}

  ${
    $d x y R $.
    erdmrn.1 $e |- Er R $.
    $( The range and domain of an equivalence relation are equal. $)
    erdmrn $p |- dom R = ran R $=
      ( vx vy cdm crn cv wbr wex wcel vex ersymb exbii eldm elrn 3bitr4i eqriv
      ) CAEZAFZCGZDGZAHZDIUATAHZDITRJTSJUBUCDTUAACKZDKBLMDTAUDNDTAUDOPQ $.
      $( [6-Aug-1995] $)
  $}

  $( Equality theorem for equivalence class. $)
  eceq1 $p |- ( A = B -> [ C ] A = [ C ] B ) $=
    ( wceq csn cima cec imaeq1 df-ec 3eqtr4g ) ABDACEZFBKFCAGCBGABKHCAICBIJ $.
    $( [23-Jul-1995] $)

  $( Equality theorem for equivalence class. $)
  eceq2 $p |- ( A = B -> [ A ] C = [ B ] C ) $=
    ( wceq csn cima cec sneq imaeq2d df-ec 3eqtr4g ) ABDZCAEZFCBEZFACGBCGLMNCAB
    HIACJBCJK $.
    $( [23-Jul-1995] $)

  ${
    $d x A $.  $d x B $.  $d x R $.
    elec.1 $e |- A e. _V $.
    elec.2 $e |- B e. _V $.
    $( Membership in an equivalence class.  Theorem 72 of [Suppes] p. 82. $)
    elec $p |- ( A e. [ B ] R <-> B R A ) $=
      ( vx cv wbr cec breq2 dfec2 elab2 ) BFGZCHBACHFABCIDMABCJFBCEKL $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x R $.  $d x A $.
    ecdmn0.1 $e |- A e. _V $.
    $( A representative of a nonempty equivalence class belongs to the domain
       of the equivalence relation. $)
    ecdmn0 $p |- ( A e. dom R <-> [ A ] R =/= (/) ) $=
      ( vx cv cec wcel wex wbr c0 wne cdm vex elec exbii n0 eldm 3bitr4ri ) DEZ
      ABFZGZDHASBIZDHTJKABLGUAUBDSABDMCNODTPDABCQR $.
      $( [28-Dec-2008] $) $( [15-Feb-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erthi.1 $e |- A e. _V $.
    erthi.2 $e |- B e. _V $.
    erthi.3 $e |- Er R $.
    $( Basic property of equivalence relations.  Part of Lemma 3N of [Enderton]
       p. 57. $)
    erthi $p |- ( A R B -> [ A ] R = [ B ] R ) $=
      ( vx wbr cec cv wcel wi ersymb vex ertr ex sylbi impbid elec 3bitr4g
      eqrdv ) ABCHZGACIZBCIZUBAGJZCHZBUECHZUEUCKUEUDKUBUFUGUBBACHZUFUGLABCDEFMU
      HUFUGBAUECEDGNZFOPQUBUGUFABUECDEUIFOPRUEACUIDSUEBCUIESTUA $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erth.1 $e |- B e. _V $.
    erth.2 $e |- Er R $.
    $( Basic property of equivalence relations.  Theorem 73 of [Suppes]
       p. 82. $)
    erth $p |- ( A e. ( dom R u. ran R ) ->
                   ( [ A ] R = [ B ] R <-> A R B ) ) $=
      ( vx cv cec wceq wbr wb cdm crn cun eceq2 eqeq1d breq1 bibi12d wcel elec
      erref eleq2 vex 3bitr3g ersym syl6bi syl5com erthi impbid1 vtoclga ) FGZC
      HZBCHZIZUKBCJZKACHZUMIZABCJZKFACLCMNZUKAIZUNUQUOURUTULUPUMUKACOPUKABCQRUK
      USSZUNUOVAUKUKCJZUNUOUKCEUAUNVBBUKCJZUOUNUKULSUKUMSVBVCULUMUKUBUKUKCFUCZV
      DTUKBCVDDTUDBUKCDVDEUEUFUGUKBCVDDEUHUIUJ $.
      $( [23-Jul-1995] $)
  $}

  ${
    erthdm.1 $e |- B e. _V $.
    erthdm.2 $e |- Er R $.
    $( Basic property of equivalence relations.  Compare Theorem 73 of [Suppes]
       p. 82.  Assumes membership in the domain instead of just the field. $)
    erthdm $p |- ( A e. dom R -> ( [ A ] R = [ B ] R <-> A R B ) ) $=
      ( cdm wcel crn cun cec wceq wbr wb elun1 erth syl ) ACFZGAQCHZIGACJBCJKAB
      CLMAQRNABCDEOP $.
      $( [28-Jul-1995] $)
  $}

  ${
    erthdmr.1 $e |- A e. _V $.
    erthdmr.2 $e |- B e. _V $.
    erthdmr.3 $e |- Er R $.
    $( Basic property of equivalence relations.  Compare Theorem 73 of [Suppes]
       p. 82.  Assumes membership of the second argument in the domain. $)
    erthdmr $p |- ( B e. dom R -> ( [ A ] R = [ B ] R <-> A R B ) ) $=
      ( cdm wcel cec wceq wbr erthdm eqcom ersymb 3bitr4g ) BCGHBCIZACIZJBACKQP
      JABCKBACDFLQPMABCDEFNO $.
      $( [30-Jul-1995] $)
  $}

  ${
    ereldm.1 $e |- A e. _V $.
    ereldm.2 $e |- B e. _V $.
    ereldm.3 $e |- Er R $.
    ereldm.4 $e |- dom R = D $.
    $( Equality of equivalence classes implies equivalence of domain
       membership. $)
    ereldm $p |- ( [ A ] R = [ B ] R -> ( A e. D <-> B e. D ) ) $=
      ( cec wceq cdm wcel wbr erthdm biimpcd ersymb breldm sylbi syl6 eleq2i
      erthdmr impbid 3bitr3g ) ADIBDIJZADKZLZBUELZACLBCLUDUFUGUDUFABDMZUGUFUDUH
      ABDFGNOUHBADMUGABDEFGPBADFQRSUDUGUHUFUGUDUHABDEFGUAOABDEQSUBUECAHTUECBHTU
      C $.
      $( [28-Jan-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erdisj.1 $e |- A e. _V $.
    erdisj.2 $e |- B e. _V $.
    erdisj.3 $e |- Er R $.
    $( Equivalence classes do not overlap.  In other words, two equivalence
       classes are either equal or disjoint.  Theorem 74 of [Suppes] p. 83. $)
    erdisj $p |- ( [ A ] R = [ B ] R \/ ( [ A ] R i^i [ B ] R ) = (/) ) $=
      ( vx cec wceq cin c0 wn cv wcel wi wal wbr vex elec ersymb bitri ex erthi
      ertr syl6 syl5bi sylbi con3rr3 alrimiv disj1 sylibr orri ) ACHZBCHZIZUMUN
      JKIZUOLZGMZUMNZURUNNZLOZGPUPUQVAGUSUTUOUSAURCQZUTUOOURACGRZDSUTURBCQZVBUO
      UTBURCQVDURBCVCESBURCEVCFTUAVBVDABCQZUOVBVDVEAURBCDVCEFUDUBABCDEFUCUEUFUG
      UHUIGUMUNUJUKUL $.
      $( [15-Jun-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    erdisj2.1 $e |- A e. _V $.
    erdisj2.2 $e |- B e. _V $.
    $( Equivalence classes do not overlap.  (Contributed by FL, 1-Jun-2007.) $)
    erdisj2 $p |- ( Er R -> ( [ A ] R = [ B ] R \/
        ( [ A ] R i^i [ B ] R ) = (/) ) ) $=
      ( wer cec wceq cin c0 wo cid cif eqeq12d ineq12d eqeq1d orbi12d ereq ider
      eceq1 elimhyp erdisj dedth ) CFZACGZBCGZHZUEUFIZJHZKAUDCLMZGZBUJGZHZUKULI
      ZJHZKCLCUJHZUGUMUIUOUPUEUKUFULCUJATZCUJBTZNUPUHUNJUPUEUKUFULUQUROPQABUJDE
      UDUJFLFCLCUJRLUJRSUAUBUC $.
      $( [1-Jun-2007] $)
  $}

  $( An equivalence class modulo the identity relation is a singleton. $)
  ecidsn $p |- [ A ] _I = { A } $=
    ( cid cec csn cima df-ec imai eqtri ) ABCBADZEIABFIGH $.
    $( [24-Oct-2004] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Equality theorem for quotient set. $)
    qseq1 $p |- ( A = B -> ( A /. C ) = ( B /. C ) ) $=
      ( vy vx wceq cv cec wrex cab cqs rexeq abbidv df-qs 3eqtr4g ) ABFZDGEGCHF
      ZEAIZDJQEBIZDJACKBCKPRSDQEABLMEDACNEDBCNO $.
      $( [23-Jul-1995] $)

    $( Equality theorem for quotient set. $)
    qseq2 $p |- ( A = B -> ( C /. A ) = ( C /. B ) ) $=
      ( vy vx wceq cec wrex cab cqs eceq1 eqeq2d rexbidv abbidv df-qs 3eqtr4g
      cv ) ABFZDQZEQZAGZFZECHZDISTBGZFZECHZDICAJCBJRUCUFDRUBUEECRUAUDSABTKLMNED
      CAOEDCBOP $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    elqs.1 $e |- B e. _V $.
    $( Membership in a quotient set. $)
    elqs $p |- ( B e. ( A /. R ) <-> E. x e. A B = [ x ] R ) $=
      ( vy cv cec wceq wrex cqs eqeq1 rexbidv df-qs elab2 ) FGZAGDHZIZABJCQIZAB
      JFCBDKEPCIRSABPCQLMAFBDNO $.
      $( [12-Nov-2008] $) $( [23-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( Membership in a quotient set. $)
    elqsi $p |- ( B e. ( A /. R ) -> E. x e. A B = [ x ] R ) $=
      ( vy cv cec wceq wrex cqs eqeq1 rexbidv wcel vex elqs biimpi vtoclga ) EF
      ZAFDGZHZABIZCSHZABIECBDJZRCHTUBABRCSKLRUCMUAABRDENOPQ $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    ecelqsi.1 $e |- R e. _V $.
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsi $p |- ( B e. A -> [ B ] R e. ( A /. R ) ) $=
      ( vy vx cec cqs wcel wceq eceq2 eleq1d wrex eqid eqeq2d rcla4ev mpan2 cvv
      cv ecexg ax-mp elqs sylibr vtoclga ) ESZCGZACHZIZBCGZUGIEBAUEBJUFUIUGUEBC
      KLUEAIZUFFSZCGZJZFAMZUHUJUFUFJZUNUFNUMUOFUEAUKUEJULUFUFUKUECKOPQFAUFCCRIU
      FRIDUERCTUAUBUCUD $.
      $( [25-Jul-1995] $)
  $}

  ${
    ecopqsi.1 $e |- R e. _V $.
    ecopqsi.2 $e |- S = ( ( A X. A ) /. R ) $.
    $( "Closure" law for equivalence class of ordered pairs. $)
    ecopqsi $p |- ( ( B e. A /\ C e. A ) -> [ <. B , C >. ] R e. S ) $=
      ( wcel wa cop cxp cec opelxpi cqs ecelqsi syl6eleqr syl ) BAHCAHIBCJZAAKZ
      HZRDLZEHBCAAMTUASDNESRDFOGPQ $.
      $( [25-Mar-1996] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    $( A quotient set exists.  (Contributed by FL, 19-May-2007.) $)
    qsexg $p |- ( A e. _V -> ( A /. R ) e. _V ) $=
      ( vx vy cvv wcel cv cec wceq wa copab crn cqs opabex2g rnexg wrex cab wex
      df-rex abbii df-qs rnopab 3eqtr4ri eleq1i biimpi 3syl ) AEFCGZAFDGUGBHZIZ
      JZCDKZEFUKLZEFZABMZEFZCDAUHENUKEOUMUOULUNEUICAPZDQUJCRZDQUNULUPUQDUICASTC
      DABUAUJCDUBUCUDUEUF $.
      $( [23-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    qsex.1 $e |- A e. _V $.
    $( A quotient set exists. $)
    qsex $p |- ( A /. R ) e. _V $=
      ( cvv wcel cqs qsexg ax-mp ) ADEABFDECABGH $.
      $( [14-Aug-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.
    uniqs.1 $e |- R e. _V $.
    $( The union of a quotient set. $)
    uniqs $p |- U. ( A /. R ) = ( R " A ) $=
      ( vx vy vz cqs cuni cima cv wcel wa wex cec wceq wrex eluni vex elqs cvv
      anbi1i ancom r19.41v 3bitr4i exbii wbr elima ecexg ax-mp eleq2 elec bitri
      ceqsexv rexbii rexcom4 3bitr2ri 3bitri eqriv ) DABGZHZBAIZDJZUTKVBEJZKZVC
      USKZLZEMVCFJZBNZOZVDLZFAPZEMZVBVAKZEVBUSQVFVKEVEVDLVIFAPZVDLVFVKVEVNVDFAV
      CBERSUAVDVEUBVIVDFAUCUDUEVMVGVBBUFZFAPVJEMZFAPVLFVBBADRZUGVPVOFAVPVBVHKZV
      OVDVREVHBTKVHTKCVGTBUHUIVCVHVBUJUMVBVGBVQFRUKULUNVJFEAUOUPUQUR $.
      $( [9-Dec-2008] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    snec.1 $e |- A e. _V $.
    $( The singleton of an equivalence class. $)
    snec $p |- { [ A ] R } = ( { A } /. R ) $=
      ( vy vx cv cec wceq cab csn wrex cqs wcel wa wex df-rex elsn anbi1i exbii
      eceq2 eqeq2d ceqsexv 3bitrri abbii df-sn df-qs 3eqtr4i ) DFZABGZHZDIUHEFZ
      BGZHZEAJZKZDIUIJUNBLUJUODUOUKUNMZUMNZEOUKAHZUMNZEOUJUMEUNPUQUSEUPURUMEAQR
      SUMUJEACURULUIUHUKABTUAUBUCUDDUIUEEDUNBUFUG $.
      $( [29-Jan-1999] $)
  $}

  ${
    ecqs.1 $e |- R e. _V $.
    $( Equivalence class in terms of quotient set. $)
    ecqs $p |- [ A ] R = U. ( { A } /. R ) $=
      ( cec csn cima cqs cuni df-ec uniqs eqtr4i ) ABDBAEZFLBGHABILBCJK $.
      $( [15-Jan-2009] $) $( [29-Jan-1999] $)
  $}

  ${
    $d x R $.  $d x A $.
    0nelqs.1 $e |- dom R = A $.
    $( A quotient set doesn't contain the empty set. $)
    0nelqs $p |- -. (/) e. ( A /. R ) $=
      ( vx c0 cqs wcel cv cec wceq wrex wn cdm wne vex ecdmn0 necom bitri df-ne
      eleq2i 3bitr3i biimpi nrex elqsi mto ) EABFGEDHZBIZJZDAKUHDAUFAGZUHLZUFBM
      ZGZEUGNZUIUJULUGENUMUFBDOPUGEQRUKAUFCTEUGSUAUBUCDAEBUDUE $.
      $( [21-Mar-2007] $) $( [24-Aug-1995] $)
  $}

  ${
    ecelqsdm.1 $e |- B e. _V $.
    ecelqsdm.2 $e |- dom R = A $.
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsdm $p |- ( [ B ] R e. ( A /. R ) -> B e. A ) $=
      ( wcel cec cqs wn c0 0nelqs wceq wb ecdmn0 eleq2i bitr3i necon1bbii eleq1
      wne cdm sylbi mtbiri con4i ) BAFZBCGZACHZFZUDIZUGJUFFZACEKUHUEJLUGUIMUDUE
      JUEJSBCTZFUDBCDNUJABEOPQUEJUFRUAUBUC $.
      $( [21-Mar-2007] $) $( [30-Jul-1995] $)
  $}

  ${
    $d y A $.
    ecid.1 $e |- A e. _V $.
    $( A set is equal to its converse epsilon coset.  (Note: converse epsilon
       is not an equivalence relation.) $)
    ecid $p |- [ A ] `' _E = A $=
      ( vy cep ccnv cec cv wbr cab wcel dfec2 vex brcnv epelc bitri abbii abid2
      3eqtri ) ADEZFACGZSHZCITAJZCIACASBKUAUBCUATADHUBATDBCLZMTAUCBNOPCAQR $.
      $( [13-Aug-1995] $)
  $}

  ${
    $d x y A $.
    $( A set is equal to its quotient set mod converse epsilon.  (Note:
       converse epsilon is not an equivalence relation.) $)
    qsid $p |- ( A /. `' _E ) = A $=
      ( vy vx cep ccnv cqs cv cec wceq wrex cab wcel df-qs vex ecid eqcom bitri
      eqeq2i rexbii risset bitr4i abbii abid2 3eqtri ) ADEZFBGZCGZUEHZIZCAJZBKU
      FALZBKACBAUEMUJUKBUJUGUFIZCAJUKUIULCAUIUFUGIULUHUGUFUGCNORUFUGPQSCUFATUAU
      BBAUCUD $.
      $( [21-Mar-2007] $) $( [13-Aug-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $d x ps $.
    ectocl.1 $e |- S = ( B /. R ) $.
    ectocl.2 $e |- ( [ x ] R = A -> ( ph <-> ps ) ) $.
    ectocl.3 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of class for equivalence class. $)
    ectocl $p |- ( A e. S -> ps ) $=
      ( wcel cqs eleq2i cv cec wceq wrex elqsi wb eqcoms syl5ibcom rexlimiv syl
      sylbi ) DGKDEFLZKZBGUEDHMUFDCNZFOZPZCEQBCEDFRUIBCEUGEKAUIBJABSUHDITUAUBUC
      UD $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z ps $.
    ecoptocl.1 $e |- S = ( ( B X. C ) /. R ) $.
    ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    ecoptocl.3 $e |- ( ( x e. B /\ y e. C ) -> ph ) $.
    $( Implicit substitution of class for equivalence class of ordered pair. $)
    ecoptocl $p |- ( A e. S -> ps ) $=
      ( vz wcel cxp cqs cv cec wceq wi eleq2i wrex elqsi cop eqid eqeq2d imbi1d
      eceq2 wa wb eqcoms syl5ibcom optocl rexlimiv syl sylbi ) EINEFGOZHPZNZBIU
      REJUAUSEMQZHRZSZMUQUBBMUQEHUCVBBMUQECQZDQZUDZHRZSZBTVBBTCDUTFGUQUQUEVEUTS
      ZVGVBBVHVFVAEVEUTHUHUFUGVCFNVDGNUIAVGBLABUJVFEKUKULUMUNUOUP $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.  $d x y z w D $.  $d z w S $.
    $d x y z w R $.  $d x y ps $.  $d z w ch $.
    2ecoptocl.1 $e |- S = ( ( C X. D ) /. R ) $.
    2ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    2ecoptocl.3 $e |- ( [ <. z , w >. ] R = B -> ( ps <-> ch ) ) $.
    2ecoptocl.4 $e |- ( ( ( x e. C /\ y e. D ) /\
                      ( z e. C /\ w e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    2ecoptocl $p |- ( ( A e. S /\ B e. S ) -> ch ) $=
      ( wcel wi cv cop cec wceq imbi2d wa ex ecoptocl com12 impcom ) IMRHMRZCUJ
      BSUJCSFGIJKLMNFTZGTZUALUBIUCBCUJPUDUJUKJRULKRUEZBUMASUMBSDEHJKLMNDTZETZUA
      LUBHUCABUMOUDUNJRUOKRUEUMAQUFUGUHUGUI $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d v u C $.  $d x y z w v u D $.
    $d z w v u S $.  $d x y z w v u R $.  $d x y ps $.  $d z w ch $.
    $d v u th $.
    3ecoptocl.1 $e |- S = ( ( D X. D ) /. R ) $.
    3ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    3ecoptocl.3 $e |- ( [ <. z , w >. ] R = B -> ( ps <-> ch ) ) $.
    3ecoptocl.4 $e |- ( [ <. v , u >. ] R = C -> ( ch <-> th ) ) $.
    3ecoptocl.5 $e |- ( ( ( x e. D /\ y e. D ) /\
                      ( z e. D /\ w e. D ) /\ ( v e. D /\ u e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    3ecoptocl $p |- ( ( A e. S /\ B e. S /\ C e. S ) -> th ) $=
      ( wcel wa wi cop cec wceq imbi2d 3expib ecoptocl com12 2ecoptocl 3impib
      cv ) KPUBZLPUBZMPUBZDUPUQUCUODUOBUDUOCUDUODUDGHIJLMNNOPQGUNZHUNZUEOUFLUGB
      CUOSUHIUNZJUNZUEOUFMUGCDUOTUHUOURNUBUSNUBUCZUTNUBVANUBUCZUCZBVDAUDVDBUDEF
      KNNOPQEUNZFUNZUEOUFKUGABVDRUHVENUBVFNUBUCVBVCAUAUIUJUKULUKUM $.
      $( [9-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u S $.  $d x y H $.  $d z w v u G $.
    $d x y ph $.  $d z w v u ps $.
    brecop.1 $e |- S e. _V $.
    brecop.2 $e |- Er S $.
    brecop.3 $e |- dom S = ( G X. G ) $.
    brecop.4 $e |- H = ( ( G X. G ) /. S ) $.
    brecop.5 $e |- R = { <. x , y >. | ( ( x e. H /\ y e. H ) /\
                      E. z E. w E. v E. u ( ( x = [ <. z , w >. ] S /\
                      y = [ <. v , u >. ] S ) /\ ph ) ) } $.
    brecop.6 $e |- ( ( ( ( z e. G /\ w e. G ) /\ ( A e. G /\ B e. G ) ) /\
                       ( ( v e. G /\ u e. G ) /\ ( C e. G /\ D e. G ) ) ) ->
                     ( ( [ <. z , w >. ] S = [ <. A , B >. ] S /\
                         [ <. v , u >. ] S = [ <. C , D >. ] S ) ->
                     ( ph <-> ps ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number
       construction. $)
    brecop $p |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                ( [ <. A , B >. ] S R [ <. C , D >. ] S <-> ps ) ) $=
      ( wcel wa cop cec wbr cv wceq wex ecopqsi copab df-br eleq2i bitri anbi1d
      eleq1 eqeq1 4exbidv anbi12d anbi2d opelopabg bianabs syl5bb syl2an opeq12
      wb eceq2 syl anim12i cxp opelxpi vex opelxp opex syl5bbr syl5ibr im2anan9
      wi ereldm an4s com13 mpdd pm5.74d cgsex4g eqcom anbi12i a1i biimt 3bitr4d
      ex bitrd ) IOUCJOUCUDZKOUCLOUCUDZUDZIJUEZNUFZKLUEZNUFZMUGZWQEUHZFUHZUEZNU
      FZUIZWSGUHZHUHZUEZNUFZUIZUDZAUDZHUJGUJFUJEUJZBWMWQPUCZWSPUCZWTXMVGWNOIJNP
      QTUKOKLNPQTUKWTWQWSUEZCUHZPUCZDUHZPUCZUDZXQXDUIZXSXIUIZUDZAUDZHUJGUJFUJEU
      JZUDZCDULZUCZXNXOUDZXMWTXPMUCYIWQWSMUMMYHXPUAUNUOYJYIXMYGXNXTUDZXEYCUDZAU
      DZHUJGUJFUJEUJZUDYJXMUDCDWQWSPPXQWQUIZYAYKYFYNYOXRXNXTXQWQPUQUPYOYEYMEFGH
      YOYDYLAYOYBXEYCXQWQXDURUPUPUSUTXSWSUIZYKYJYNXMYPXTXOXNXSWSPUQVAYPYMXLEFGH
      YPYLXKAYPYCXJXEXSWSXIURVAUPUSUTVBVCVDVEWOXDWQUIZXIWSUIZUDZWOAVSZUDZHUJGUJ
      FUJEUJWOBVSZXMBYTUUBYSEFGHIJKLOOXAIUIXBJUIUDZYQXFKUIXGLUIUDZYRUUCXCWPUIYQ
      XAXBIJVFXCWPNVHVIUUDXHWRUIYRXFXGKLVFXHWRNVHVIVJYSWOABYSWOXAOUCXBOUCUDZXFO
      UCXGOUCUDZUDZABVGZYQWMUUEYRWNUUFWMUUEYQWPOOVKZUCZIJOOVLUUEXCUUIUCYQUUJXAX
      BOOFVMVNXCWPUUINXAXBVOIJVORSVTVPVQWNUUFYRWRUUIUCZKLOOVLUUFXHUUIUCYRUUKXFX
      GOOHVMVNXHWRUUINXFXGVOKLVORSVTVPVQVRUUGWOYSUUHUUGWOYSUUHVSZUUEWMUUFWNUULU
      BWAWKWBWCWDWEWOXLUUAEFGHWOXKYSAYTXKYSVGWOXEYQXJYRWQXDWFWSXIWFWGWHWOAWIUTU
      SWOBWIWJWL $.
      $( [29-Jan-1996] $)
  $}

  ${
    brecop2.1 $e |- S e. _V $.
    brecop2.2 $e |- B e. _V $.
    brecop2.3 $e |- C e. _V $.
    brecop2.4 $e |- D e. _V $.
    brecop2.5 $e |- dom S = ( G X. G ) $.
    brecop2.6 $e |- H = ( ( G X. G ) /. S ) $.
    brecop2.7 $e |- R C_ ( H X. H ) $.
    brecop2.8 $e |- Q C_ ( G X. G ) $.
    brecop2.9 $e |- -. (/) e. G $.
    brecop2.10 $e |- dom F = ( G X. G ) $.
    brecop2.11 $e |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                    ( [ <. A , B >. ] S R [ <. C , D >. ] S <->
                    ( A F D ) Q ( B F C ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number construction.
       Eliminates antecedent from last hypothesis. $)
    brecop2 $p |- ( [ <. A , B >. ] S R [ <. C , D >. ] S <->
                    ( A F D ) Q ( B F C ) ) $=
      ( cop cec wbr wcel wa co cvv ecexg ax-mp brel cxp cqs opex ecelqsdm sylbi
      eleq2i opelxp sylib anim12i syl ovex ndmovrcl an42 pm5.21nii ) ABUBZGUCZC
      DUBZGUCZFUDZAIUEZBIUEZUFZCIUEZDIUEZUFZUFZADHUGZBCHUGZEUDZVJVGJUEZVIJUEZUF
      VQVGVIJJFGUHUEVIUHUEKVHUHGUIUJQUKWAVMWBVPWAVFIIULZUEZVMWAVGWCGUMZUEWDJWEV
      GPUQWCVFGABUNOUOUPABIILURUSWBVHWCUEZVPWBVIWEUEWFJWEVIPUQWCVHGCDUNOUOUPCDI
      INURUSUTVAVTVKVOUFZVLVNUFZUFZVQVTVRIUEZVSIUEZUFWIVRVSIIEBCHVBRUKWJWGWKWHA
      DIHNTSVCBCIHMTSVCUTVAVKVOVLVNVDUSUAVE $.
      $( [21-Mar-2007] $) $( [13-Feb-1996] $)
  $}

  ${
    $d f g h t s r A $.  $d f g h t s r B $.  $d f g h t s r C $.
    $d f g h t s r D $.  $d x y z w v u f g h t s r F $.  $d f g h t s r R $.
    $d x y z w v u f g h t s r S $.
    ecopopr.1 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\ y e. ( S X. S )
          ) /\ E. z E. w E. v E. u ( ( x = <. z , w >. /\ y = <. v , u >. ) /\
          ( z F u ) = ( w F v ) ) ) } $.
    ${
      $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
      $d x y z w v u D $.
      $( This is the first of several theorems about equivalence relations of
         the kind used in construction of fractions and signed reals, involving
         operations on equivalent classes of ordered pairs.  This theorem
         expresses the relation ` R ` (specified by the hypothesis) in terms of
         its operation ` F ` . $)
      ecopoveq $p |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $=
        ( cv co wceq wb wa oveq12 eqeqan12d an42s opbrop ) COZFOZMPZDOZEOZMPZQZ
        GJMPZHIMPZQZABCDEFGHIJKLUDGQZUEJQZUGHQZUHIQZUJUMRUNUOSUPUQSUFUKUIULUDGU
        EJMTUGHUHIMTUAUBNUC $.
        $( [16-Aug-1995] $)
    $}

    ${
      ecopopr.com $e |- ( x F y ) = ( y F x ) $.
      $( Assuming the operation ` F ` is commutative, compute the domain the
         relation ` R ` specified by the first hypothesis. $)
      ecopovdm $p |- dom R = ( S X. S ) $=
        ( vf vg cv wcel wa cop wceq co wex cdm cxp copab opabssxp eqsstri ax-mp
        wss dmss dmxpid sseqtri relxp vex opelxp wbr caovcom wb ecopoveq anidms
        mpbiri df-br sylib opex opeldm syl sylbi relssi eqssi ) GUAZHHUBZVHVIVI
        UBZUAZVIGVJUGVHVKUGGANZVIOBNZVIOPVLCNZDNZQRVMENZFNZQRPVNVQISVOVPISRPFTE
        TDTCTZPABUCVJJVRABVIVIUDUEGVJUHUFVIUIUJLMVIVHHHUKLNZMNZQZVIOVSHOVTHOPZW
        AVHOZVSVTHHMULZUMWBWAWAQGOZWCWBWAWAGUNZWEWBWFVSVTISVTVSISRZABVSVTILULWD
        KUOWBWFWGUPABCDEFVSVTVSVTGHIJUQURUSWAWAGUTVAWAWAGVSVTVBVCVDVEVFVG $.
        $( [27-Aug-1995] $)

      ${
        ecopopr.2 $e |- B e. _V $.
        $( Assuming the operation ` F ` is commutative, show that the relation
           ` R ` , specified by the first hypothesis, is symmetric. $)
        ecopovsym $p |- ( A R B -> B R A ) $=
          ( wbr wcel wa cv wceq co vf vg vh vt cxp wb cop opabssxp eqsstri brel
          wex copab eqid breq1 breq2 bibi12d ecopoveq vex caovcom eqeq12i eqcom
          bitri syl6bb ancoms bitr4d 2optocl syl ibi ) GHIOZHGIOZVIGJJUEZPHVKPQ
          VIVJUFZGHVKVKINIARZVKPBRZVKPQVMCRZDRZUGSVNERZFRZUGSQVOVRKTVPVQKTSQFUK
          EUKDUKCUKZQABULVKVKUELVSABVKVKUHUIUJUARZUBRZUGZUCRZUDRZUGZIOZWEWBIOZU
          FGWEIOZWEGIOZUFVLUAUBUCUDGHJJVKVKUMWBGSWFWHWGWIWBGWEIUNWBGWEIUOUPWEHS
          WHVIWIVJWEHGIUOWEHGIUNUPVTJPWAJPQZWCJPWDJPQZQZWFWCWAKTZWDVTKTZSZWGWLW
          FVTWDKTZWAWCKTZSZWOABCDEFVTWAWCWDIJKLUQWRWNWMSWOWPWNWQWMABVTWDKUAURUD
          URMUSABWAWCKUBURUCURMUSUTWNWMVAVBVCWKWJWGWOUFABCDEFWCWDVTWAIJKLUQVDVE
          VFVGVH $.
          $( [27-Aug-1995] $)
      $}

      ${
        $( Assume the operation is closed. $)
        ecopopr.cl $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
        $( Assume the operation is associative. $)
        ecopopr.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
        $( Assume the operation has the cancellation property. $)
        ecopopr.can $e |- ( ( x e. S /\ y e. S ) ->
                   ( ( x F y ) = ( x F z ) -> y = z ) ) $.
        ${
          ecopopr.3 $e |- B e. _V $.
          ecopopr.4 $e |- C e. _V $.
          $( Assuming that operation ` F ` is commutative (second hypothesis),
             closed (third hypothesis), associative (fourth hypothesis), and
             has the cancellation property (fifth hypothesis), show that the
             relation ` R ` , specified by the first hypothesis, is
             transitive. $)
          ecopovtrn $p |- ( ( A R B /\ B R C ) -> A R C ) $=
            ( co vf vg vh vt vs vr cxp wcel w3a wbr wa cv cop wceq wex opabssxp
            copab eqsstri brel simpld anim12i 3anass sylibr eqid anbi1d imbi12d
            wi breq1 breq2 anbi12d imbi1d anbi2d wb ecopoveq 3adant3 oveq12 vex
            3adant1 caov411 caov4 eqtr3i syl6bi caovcl ovex syl2an 3impb 3com12
            3eqtr4g caovcan 3adant3l 3adant1r 3adant2 sylibrd 3optocl mpcom
            syld ) GKKUGZUHZHWQUHZIWQUHZUIZGHJUJZHIJUJZUKZGIJUJZXDWRWSWTUKZUKXA
            XBWRXCXFXBWRWSGHWQWQJRJAULZWQUHBULZWQUHUKXGCULZDULZUMUNXHEULZFULZUM
            UNUKXIXLLTXJXKLTUNUKFUOEUODUOCUOZUKABUQWQWQUGMXMABWQWQUPURZUSUTHIWQ
            WQJSXNUSVAWRWSWTVBVCUAULZUBULZUMZUCULZUDULZUMZJUJZXTUEULZUFULZUMZJU
            JZUKZXQYDJUJZVGGXTJUJZYEUKZGYDJUJZVGXBHYDJUJZUKZYJVGXDXEVGUAUBUCUDU
            EUFGHIKWQKWQVDXQGUNZYFYIYGYJYMYAYHYEXQGXTJVHVEXQGYDJVHVFXTHUNZYIYLY
            JYNYHXBYEYKXTHGJVIXTHYDJVHVJVKYDIUNZYLXDYJXEYOYKXCXBYDIHJVIVLYDIGJV
            IVFXOKUHZXPKUHZUKZXRKUHXSKUHUKZYBKUHZYCKUHZUKZUIZYFXOYCLTZXPYBLTZUN
            ZYGUUCYFXRXSLTZUUDLTZUUGUUELTZUNZUUFUUCYFXOXSLTZXPXRLTZUNZXRYCLTZXS
            YBLTZUNZUKZUUJUUCYAUUMYEUUPYRYSYAUUMVMUUBABCDEFXOXPXRXSJKLMVNVOYSUU
            BYEUUPVMYRABCDEFXRXSYBYCJKLMVNVRVJUUQUUKUUNLTUULUUOLTZUUHUUIUUKUULU
            UNUUOLVPABCXRXSXOYCLUCVQZUDVQZUAVQNPUFVQVSXPXSLTXRYBLTLTUUIUURABCXP
            XSXRYBLUBVQZUUTUUSNPUEVQZVSABCXPXSXRYBLUVAUUTUUSNPUVBVTWAWHWBYPYSUU
            BUUJUUFVGZYQYPYSUUAUVCYTYSYPUUAUVCYSYPUUAUVCYSUUGKUHUUDKUHUVCYPUUAU
            KABXRXSKLOWCABXOYCKLOWCABCUUGUUDUUEKLXPYBLWDQWIWEWFWGWJWKWPYRUUBYGU
            UFVMYSABCDEFXOXPYBYCJKLMVNWLWMWNWO $.
            $( [11-Feb-1996] $)
        $}

        $( Assuming that operation ` F ` is commutative (second hypothesis),
           closed (third hypothesis), associative (fourth hypothesis), and has
           the cancellation property (fifth hypothesis), show that the relation
           ` R ` , specified by the first hypothesis, is an equivalence
           relation. $)
        ecopover $p |- Er R $=
          ( vf vg vh cv vex ecopovsym ecopovtrn ster ) OPQGABCDEFORZPRZGHIJKPSZ
          TABCDEFUCUDQRGHIJKLMNUEQSUAUB $.
          $( [16-Feb-1996] $)
      $}

    $}
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y A $.  $d x y B $.  $d x y C $.
    $d x y D $.
    eceqoveq.2 $e |- B e. _V $.
    $( (We don't bother to eliminate this redundant hypothesis.) $)
    eceqoveq.3 $e |- C e. _V $.
    eceqoveq.4 $e |- D e. _V $.
    eceqoveq.5 $e |- Er R $.
    eceqoveq.6 $e |- dom R = ( S X. S ) $.
    eceqoveq.7 $e |- dom F = ( S X. S ) $.
    eceqoveq.8 $e |- -. (/) e. S $.
    eceqoveq.9 $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
    eceqoveq.10 $e |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $.
    $( Equality of equivalence relation in terms of an operation. $)
    eceqoveq $p |- ( ( A e. S /\ C e. S ) ->
                      ( [ <. A , B >. ] R = [ <. C , D >. ] R <->
                      ( A F D ) = ( B F C ) ) ) $=
      ( wcel c0 wa cop cec wceq co wb wi w3a wbr cdm cxp opelxpi syl6eleqr opex
      simpl erthdm 3syl bitrd exp43 wn wne eleq2i ecdmn0 opelxp 3bitr3i simprbi
      3imp necon1bi eqeq1 biimprcd necon3d syl5bir com12 3adant3 caovcl syl5ibr
      syl eleq1 ndmovrcl simprd syl6com necon3bd 3adant1 jcad necon4bid pm2.61d
      pm5.1 syl6 3exp com23 imp biimpcd syl5ib simpld 3expia eqeq2d con3i ndmov
      3adant2 simpr eqeq2 pm2.61d1 ) CHSZEHSZUAZDHSZCDUBZGUCZEFUBZGUCZUDZCFIUEZ
      DEIUEZUDZUFZXCXDXFXOUGXCXFXDXOXCXFXDXOXCXFXDUHZFHSZXOXCXFXDXQXOUGXCXFXDXQ
      XOXCXFUAZXDXQUAZUAZXKXGXIGUIZXNXTXRXGGUJZSZXKYAUFXRXSUOXRXGHHUKZYBCDHHULN
      UMXGXIGEFUNZMUPUQRURUSVGXPXQUTZXHXJVAZXLXMVAZUAZXOXPYFYGYHXCXFYFYGUGXDYFX
      RYGXRXHTVAZYFYGYCXGYDSYJXRYBYDXGNVBXGGCDUNVCCDHHJVDVEZYFXJTUDZYJYGUGXQXJT
      XJTVAZXDXQXIYBSXIYDSYMXSYBYDXINVBXIGYEVCEFHHLVDVEZVFVHZYLXHXJXHTXKXHTUDZY
      LXHXJTVIZVJVKVQVLVMVNXFXDYFYHUGXCXFXDUAZXQXLXMXNYRXLHSZXQYRYSXNXMHSZABDEH
      IQVOXLXMHVRZVPYSXCXQCFHILOPVSVTWAWBWCWDYIXHXJXLXMYGYHWGWEZWHWFWIWJWKXEXQX
      FUTZXOUGZXCXDXQUUDXCXDXQUHZUUCYIXOUUEUUCYGYHXDXQUUCYGUGXCUUCXSYGXSYMUUCYG
      YNUUCYPYMYGUGXFXHTYJXCXFYKVFVHZYPXHXJXJTXKYPYLYQWLVKVQVLVMWCXCXQUUCYHUGXD
      XCXQUAZXFXLXMXNUUGYTXFUUGYSXNYTABCFHIQVOUUAWMYTXFXDDEHIKOPVSWNWAWBWSWDUUB
      WHWOYFUUCXKXNUAXOYFUUCXKXNUUCXKYFYPUUFYFXJTXHYOWPVPUUCYFXNUUCYRUTXMTUDZYF
      XNUGYRXFXFXDUOWQDEHIKOWRYFXNUUHXLTUDZYFUUGUTUUIUUGXQXCXQWTWQCFHILOWRVQXMT
      XLXAVPUQVMWDXKXNWGWHXBWF $.
      $( [15-Feb-1996] $)
  $}

  ${
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d x y z w v u A $.  $d x y z w v u B $.
    th3qlem1.1 $e |- Er R $.
    th3qlem1.2 $e |- dom R = S $.
    $( Compatibility assumption. $)
    th3qlem1.3 $e |- ( ( ( y e. S /\ w e. S ) /\ ( z e. S /\ v e. S ) ) ->
                 ( ( y R w /\ z R v ) -> ( y F z ) R ( w F v ) ) ) $.
    $( Lemma for Exercise 44 version of Theorem 3Q of [Enderton] p. 60.  The
       third hypothesis is the compatibility assumption. $)
    th3qlem1 $p |- ( ( A e. ( S /. R ) /\ B e. ( S /. R ) ) -> E* x
       E. y E. z ( ( A = [ y ] R /\ B = [ z ] R ) /\
       x = [ ( y F z ) ] R ) ) $=
      ( wcel wa cv cec wceq wex an4 vu cqs co wi wal ee4anv anidm anbi12i eleq1
      wmo anbi2i pm5.32ri bitri 3bitri vex ecelqsdm anim12i eqtr2 wbr cdm simpl
      syl6eleqr erthdm syl bi2anan9 sylbid ovex erthi eqeq12 syl5ibr syl9 com23
      wb imp syl2an an4s syl2anb sylbi sylanbr expimpd syl5bi exlimdvv alrimivv
      syl5bir eqeq1 anbi2d 2exbidv eqeq2d oveq12 anbi12d cbvex2v syl6bb sylibr
      eceq2 mo4 ) FIHUBZNZGWPNZOZFBPZHQZRZGCPZHQZRZOZAPZWTXCJUCZHQZRZOZCSBSZFDP
      ZHQZRZGEPZHQZRZOZUAPZXMXPJUCZHQZRZOZESDSZOZXGXTRZUDZUAUEAUEXLAUJWSYHAUAYF
      XKYDOZESDSZCSBSWSYGXKYDBCDEUFWSYJYGBCWSYIYGDEYIXFXSOZXJYCOZOWSYGXFXJXSYCT
      WSYKYLYGWSWQWQOZWRWROZOZYKYLYGUDZYMWQYNWRWQUGWRUGUHYOYKOZXAWPNZXBOZXNWPNZ
      XOOZOZXDWPNZXEOZXQWPNZXROZOZOZYPYQYOXBXOOZXEXROZOZOYMUUIOZYNUUJOZOUUHYKUU
      KYOXBXEXOXRTUKYMYNUUIUUJTUULUUBUUMUUGUULWQXBOZWQXOOZOUUBWQWQXBXOTUUNYSUUO
      UUAXBWQYRFXAWPUIULXOWQYTFXNWPUIULUHUMUUMWRXEOZWRXROZOUUGWRWRXEXRTUUPUUDUU
      QUUFXEWRUUCGXDWPUIULXRWRUUEGXQWPUIULUHUMUHUNUUBYRYTOZUUIOUUCUUEOZUUJOYPUU
      GYRXBYTXOTUUCXEUUEXRTUURUUSUUIUUJYPUURUUSOWTINZXMINZOZXCINZXPINZOZOZXAXNR
      ZXDXQRZOZYPUUKUURUVBUUSUVEYRUUTYTUVAIWTHBUOLUPIXMHDUOZLUPUQUUCUVCUUEUVDIX
      CHCUOLUPIXPHEUOZLUPUQUQUUIUVGUUJUVHFXAXNURGXDXQURUQUVFUVIYPUVFYLUVIYGUVFU
      VIXHYAHUSZYLYGUVFUVIWTXMHUSZXCXPHUSZOUVLUVBUVGUVMUVEUVHUVNUVBWTHUTZNUVGUV
      MVMUVBWTIUVOUUTUVAVALVBWTXMHUVJKVCVDUVEXCUVONUVHUVNVMUVEXCIUVOUVCUVDVALVB
      XCXPHUVKKVCVDVEMVFUVLYGYLXIYBRZXHYAHWTXCJVGXMXPJVGKVHXGXIXTYBVIVJVKVLVNVO
      VPVQVRVSVTWAWBWBWDWCXLYEAUAYGXLXFXTXIRZOZCSBSYEYGXKUVRBCYGXJUVQXFXGXTXIWE
      WFWGUVRYDBCDEWTXMRZXCXPRZOZXFXSUVQYCUVSXBXOUVTXEXRUVSXAXNFWTXMHWNWHUVTXDX
      QGXCXPHWNWHVEUWAXIYBXTUWAXHYARUVPWTXMXCXPJWIXHYAHWNVDWHWJWKWLWOWM $.
      $( [3-Aug-1995] $)
  $}

  ${
    $d x y z w v u t s f g h R $.  $d x y z w v u t s f g h S $.
    $d x y z w v u t s f A $.  $d x y z w v u t s f B $.  $d x y z w v u t C $.
    $d x y z w v u t D $.  $d x y z w v u t s f g h F $.
    th3q.1 $e |- R e. _V $.
    th3q.2 $e |- Er R $.
    th3q.3 $e |- dom R = ( S X. S ) $.
    $( Compatibility assumption. $)
    th3q.4 $e |- ( ( ( ( w e. S /\ v e. S ) /\ ( u e. S /\ t e. S ) )
       /\ ( ( s e. S /\ f e. S ) /\ ( g e. S /\ h e. S ) ) ) ->
       ( ( <. w , v >. R <. u , t >. /\ <. s , f >. R <. g , h >. ) ->
       ( <. w , v >. F <. s , f >. ) R ( <. u , t >. F <. g , h >. ) ) ) $.
    $( Lemma for Exercise 44 version of Theorem 3Q of [Enderton] p. 60,
       extended to operations on ordered pairs.  The fourth hypothesis is the
       compatibility assumption. $)
    th3qlem2 $p |- ( ( A e. ( ( S X. S ) /. R ) /\ B e. ( ( S X. S ) /. R ) )
           -> E* z E. w E. v E. u E. t (
           ( A = [ <. w , v >. ] R /\ B = [ <. u , t >. ] R ) /\
                  z = [ ( <. w , v >. F <. u , t >. ) ] R ) ) $=
      ( wcel wa vx vy cxp cqs cv cec wceq co wex wmo cop wbr breq1 anbi1d oveq1
      eqid breq1d imbi12d imbi2d breq2 breq2d anbi2d oveq2 expcom 2optocl com12
      wi imp th3qlem1 eceq2 eqeq2d bi2anan9 oveq12 syl anbi12d cla42ev exlimivv
      opex immoi ) FIIUCZHUDZSGWASTFNUEZHUFZUGZGUAUEZHUFZUGZTZAUEZWBWEMUHZHUFZU
      GZTZUAUINUIZAUJFBUEZCUEZUKZHUFZUGZGDUEZEUEZUKZHUFZUGZTZWIWQXBMUHZHUFZUGZT
      ZEUIDUIZCUIBUIZAUJANUAJUBFGHVTMPQWBVTSJUEZVTSTWEVTSUBUEZVTSTZWBXLHULZWEXM
      HULZTZWJXLXMMUHZHULZVGZXNWQXBHULZXPTZWQWEMUHZXBXMMUHZHULZVGZVGXNWBXBHULZX
      PTZWJYDHULZVGZVGXNXTVGBCDEWBXLIIVTVTUPZWQWBUGZYFYJXNYLYBYHYEYIYLYAYGXPWQW
      BXBHUMUNYLYCWJYDHWQWBWEMUOUQURUSXBXLUGZYJXTXNYMYHXQYIXSYMYGXOXPXBXLWBHUTU
      NYMYDXRWJHXBXLXMMUOVAURUSXNWOISWPISTWTISXAISTTZYFYNYAWBXLUKZKUEZLUEZUKZHU
      LZTZWQYOMUHZXBYRMUHZHULZVGZVGYNYAWEYRHULZTZYCUUBHULZVGZVGYNYFVGNJKLWEXMII
      VTYKYOWEUGZUUDUUHYNUUIYTUUFUUCUUGUUIYSUUEYAYOWEYRHUMVBUUIUUAYCUUBHYOWEWQM
      VCUQURUSYRXMUGZUUHYFYNUUJUUFYBUUGYEUUJUUEXPYAYRXMWEHUTVBUUJUUBYDYCHYRXMXB
      MVCVAURUSYNWBISXLISTYPISYQISTTUUDRVDVEVFVEVHVIXKWNAXJWNBCXIWNDEWMXINUAWQX
      BWOWPVRWTXAVRWBWQUGZWEXBUGZTZWHXEWLXHUUKWDWSUULWGXDUUKWCWRFWBWQHVJVKUULWF
      XCGWEXBHVJVKVLUUMWKXGWIUUMWJXFUGWKXGUGWBWQWEXBMVMWJXFHVJVNVKVOVPVQVQVSVN
      $.
      $( [4-Aug-1995] $)

    ${
      th3q.5 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( ( S X. S ) /. R )
           /\ y e. ( ( S X. S ) /. R ) ) /\ E. w E. v E. u E. t ( (
           x = [ <. w , v >. ] R /\ y = [ <. u , t >. ] R ) /\
           z = [ ( <. w , v >. F <. u , t >. ) ] R ) ) } $.
      $( Corollary of Theorem 3Q of [Enderton] p. 60.  (Unnecessary distinct
         variable restrictions were removed by David Abernethy, 4-Jun-2013.) $)
      th3qcor $p |- Fun G $=
        ( wfun cv cxp cqs wcel wa cop cec co wex copab2 wmo wi th3qlem2 moanimv
        wceq mpbir funoprab wb funeq ax-mp ) NUAZAUBZIIUCHUDZUEBUBZVDUEUFZVCDUB
        EUBUGZHUHUPVEFUBGUBUGZHUHUPUFCUBVGVHMUIHUHUPUFGUJFUJEUJDUJZUFZABCUKZUAZ
        VJABCVJCULVFVICULUMCDEFGVCVEHIJKLMOPQRSUNVFVICUOUQURNVKUPVBVLUSTNVKUTVA
        UQ $.
        $( [4-Jun-2013] $)   $( [12-Nov-1995] $)

      $( Theorem 3Q of [Enderton] p. 60, extended to operations on ordered
         pairs.  (Unnecessary distinct variable restrictions were removed by
         David Abernethy, 4-Jun-2013.) $)
      th3q $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                 ( [ <. A , B >. ] R G [ <. C , D >. ] R ) =
                 [ ( <. A , B >. F <. C , D >. ) ] R ) $=
        ( wcel wa cop cec cxp cqs cv wceq co wex opelxpi ecelqsi anim12i pm3.2i
        syl eqid opeq12 eceq2 eqeq2d anbi1d oveq1 anbi12d cla42egv anbi2d oveq2
        wb 2eximdv sylan9 mp2ani cvv wi ecexg ax-mp w3a eqeq1 bi2anan9 th3qlem2
        3impa 4exbidv ovig mp3an3 sylc ) HMUEIMUEUFZJMUEKMUEUFZUFZHIUGZLUHZMMUI
        ZLUJZUEZJKUGZLUHZWMUEZUFWKDUKZEUKZUGZLUHZULZWPFUKZGUKZUGZLUHZULZUFZWJWO
        QUMZLUHZWTXEQUMZLUHZULZUFZGUNFUNZEUNDUNZWKWPRUMXJULZWGWNWHWQWGWJWLUEWNH
        IMMUOWLWJLTUPUSWHWOWLUEWQJKMMUOWLWOLTUPUSUQWIWKWKULZWPWPULZUFZXJXJULZXP
        XRXSWKUTWPUTURXJUTWGXTYAUFZXBXSUFZXJWTWOQUMZLUHZULZUFZEUNDUNWHXPYGYBDEH
        IMMWRHULWSIULUFWTWJULZYGYBVJWRWSHIVAYHYCXTYFYAYHXBXRXSYHXAWKWKWTWJLVBVC
        VDYHYEXJXJYHYDXIULYEXJULWTWJWOQVEYDXILVBUSVCVFUSVGWHYGXODEXNYGFGJKMMXCJ
        ULXDKULUFXEWOULZXNYGVJXCXDJKVAYIXHYCXMYFYIXGXSXBYIXFWPWPXEWOLVBVCVHYIXL
        YEXJYIXKYDULXLYEULXEWOWTQVIXKYDLVBUSVCVFUSVGVKVLVMWNWQXJVNUEZXPXQVOLVNU
        EYJTXIVNLVPVQAUKZXAULZBUKZXFULZUFZCUKZXLULZUFZGUNFUNEUNDUNXPABCWKWPXJVN
        WMWMRYKWKULZYMWPULZYPXJULZVRYRXNDEFGYSYTUUAYRXNVJYSYTUFYOXHUUAYQXMYSYLX
        BYTYNXGYKWKXAVSYMWPXFVSVTYPXJXLVSVTWBWCCDEFGYKYMLMNOPQSTUAUBUCWAUDWDWEW
        F $.
        $( [19-Dec-2013] $) $( [4-Aug-1995] $)
    $}
  $}

  ${
    $d x y z w v u f g h a b c d A $.  $d x y z w v u f g h a b c d B $.
    $d x y z w v u f a b c d C $.  $d x y z w v u f a b c d D $.
    $d x y z t s g h a b c d G $.  $d x y z w v u f H $.  $d x y z J $.
    $d x y z w v u f K $.  $d x y z w v u f L $.  $d x y z t s g h a b c d R $.
    $d x y z w v u t s f g h a b c d S $.  $d x y ph $.  $d z w v u ps $.
    $d z w v u ch $.
    ovec.1 $e |- H e. _V $.
    ovec.2 $e |- K e. _V $.
    ovec.3 $e |- L e. _V $.
    ovec.4 $e |- R e. _V $.
    ovec.5 $e |- Er R $.
    ovec.6 $e |- dom R = ( S X. S ) $.
    $( Equivalence relation. $)
    ovec.7 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. z E. w E. v E. u ( ( x = <. z , w >. /\
                      y = <. v , u >. ) /\ ph ) ) } $.
    $( Substitutions for equivalence relation. $)
    ovec.8 $e |- ( ( ( z = a /\ w = b ) /\ ( v = c /\ u = d ) ) ->
                     ( ph <-> ps ) ) $.
    ovec.9 $e |- ( ( ( z = g /\ w = h ) /\ ( v = t /\ u = s ) ) ->
                     ( ph <-> ch ) ) $.
    $( Pre-operation. $)
    ovec.10 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. w E. v E. u E. f ( ( x = <. w , v >. /\
                      y = <. u , f >. ) /\ z = J ) ) } $.
    $( Substitutions for pre-operation. $)
    ovec.11 $e |- ( ( ( w = a /\ v = b ) /\ ( u = g /\ f = h ) ) -> J = K ) $.
    ovec.12 $e |- ( ( ( w = c /\ v = d ) /\ ( u = t /\ f = s ) ) -> J = L ) $.
    ovec.13 $e |- ( ( ( w = A /\ v = B ) /\ ( u = C /\ f = D ) ) -> J = H ) $.
    $( Main operation. $)
    ovec.14 $e |- F = { <. <. x , y >. , z >. | ( ( x e. Q /\ y e. Q ) /\
           E. a E. b E. c E. d ( (
           x = [ <. a , b >. ] R /\ y = [ <. c , d >. ] R ) /\
           z = [ ( <. a , b >. G <. c , d >. ) ] R ) ) } $.
    $( Quotient class for main operation. $)
    ovec.15 $e |- Q = ( ( S X. S ) /. R ) $.
    $( Lemma to establish compatibility of pre-operation. $)
    ovec.16 $e |- ( ( ( ( a e. S /\ b e. S ) /\ ( c e. S /\ d e. S ) ) /\
                       ( ( g e. S /\ h e. S ) /\ ( t e. S /\ s e. S ) ) ) ->
                   ( ( ps /\ ch ) -> K R L ) ) $.
    $( Express an operation on equivalence classes of ordered pairs in terms of
       equivalence class of operations on ordered pairs.  See set.mm for
       additional comments describing the hypotheses.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 4-Jun-2013.) $)
    ovec $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                ( [ <. A , B >. ] R F [ <. C , D >. ] R ) =
                   [ H ] R ) $=
      ( wcel wa cop cec co cv wbr opbrop bi2anan9 wb ov3 breqan12d an4s 3imtr4d
      wceq wex copab2 cxp eleq2i anbi12i anbi1i oprabbii eqtri th3q eceq2 eqtrd
      cqs syl ) KQVHLQVHVIMQVHNQVHVIVIZKLVJZPVKMNVJZPVKUAVLWQWRUBVLZPVKZUCPVKZD
      EFUHUIUJUKKLMNPQTJUGUBUASUOUPUQUHVMZQVHUIVMZQVHVIZUJVMZQVHUKVMZQVHVIZVIZS
      VMZQVHTVMZQVHVIZJVMZQVHUGVMZQVHVIZVIZVIBCVIUEUFPVNZXBXCVJZXEXFVJZPVNZXIXJ
      VJZXLXMVJZPVNZVIXQXTUBVLZXRYAUBVLZPVNZVGXHXSBXOYBCABDEFGHIXBXCXEXFPQUSURV
      OACDEFGHIXIXJXLXMPQUTURVOVPXDXKXGXNYEXPVQXDXKVIXGXNVIYCUEYDUFPDEFGHIXBXCX
      IXJUDUERUBQUMVBVAVRDEFGHIXEXFXLXMUDUFRUBQUNVCVAVRVSVTWAUADVMZOVHZEVMZOVHZ
      VIZYFXQPVKWBYHXRPVKWBVIFVMXQXRUBVLPVKWBVIUKWCUJWCUIWCUHWCZVIZDEFWDYFQQWEP
      WNZVHZYHYMVHZVIZYKVIZDEFWDVEYLYQDEFYJYPYKYGYNYIYOOYMYFVFWFOYMYHVFWFWGWHWI
      WJWKWPWSUCWBWTXAWBDEFGHIKLMNUDUCRUBQULVDVAVRWSUCPWLWOWM $.
      $( [4-Jun-2013] $) $( [6-Aug-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w F $.  $d x y z w R $.
    $d x y z w S $.  $d z w C $.
    ecovcom.1 $e |- C = ( ( S X. S ) /. R ) $.
    $( D,G for AFB $)
    ecovcom.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. z , w >. ] R ) = [ <. D , G >. ] R ) $.
    $( H,J for BFA $)
    ecovcom.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( x e. S /\ y e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. x , y >. ] R ) = [ <. H , J >. ] R ) $.
    ecovcom.4 $e |- D = H $.
    ecovcom.5 $e |- G = J $.
    $( Lemma used to transfer a commutative law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovcom $p |- ( ( A e. C /\ B e. C ) -> ( A F B ) = ( B F A ) ) $=
      ( wceq cv cop cec co oveq1 oveq2 eqeq12d wa opeq12 eceq2 syl mp2an ancoms
      wcel 3eqtr4a 2ecoptocl ) AUAZBUAZUBIUCZCUAZDUAZUBIUCZKUDZVBUSKUDZTEVBKUDZ
      VBEKUDZTEFKUDZFEKUDZTABCDEFJJIGOUSETVCVEVDVFUSEVBKUEUSEVBKUFUGVBFTVEVGVFV
      HVBFEKUFVBFEKUEUGUQJUNURJUNUHZUTJUNVAJUNUHZUHHLUBZIUCZMNUBZIUCZVCVDHMTZLN
      TZVLVNTZRSVOVPUHVKVMTVQHLMNUIVKVMIUJUKULPVJVIVDVNTQUMUOUP $.
      $( [4-Jun-2013] $) $( [29-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d x y z w v u C $.
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d z w v u D $.
    ecovass.1 $e |- D = ( ( S X. S ) /. R ) $.
    ecovass.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. z , w >. ] R ) = [ <. G , H >. ] R ) $.
    ecovass.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. v , u >. ] R ) = [ <. N , Q >. ] R ) $.
    ecovass.4 $e |- ( ( ( G e. S /\ H e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. G , H >. ] R F [ <. v , u >. ] R ) = [ <. J , K >. ] R ) $.
    ecovass.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( N e. S /\ Q e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. N , Q >. ] R ) = [ <. L , M >. ] R ) $.
    ecovass.6 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( G e. S /\ H e. S ) ) $.
    ecovass.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( N e. S /\ Q e. S ) ) $.
    ecovass.8 $e |- J = L $.
    ecovass.9 $e |- K = M $.
    $( Lemma used to transfer an associative law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovass $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( ( A F B ) F C ) = ( A F ( B F C ) ) ) $=
      ( cv cop cec co wceq oveq1 oveq1d eqeq12d oveq2 oveq2d wcel wa w3a opeq12
      eceq2 syl mp2an adantr sylan eqtrd 3impa adantl sylan2 3eqtr4a 3ecoptocl
      3impb ) AUKZBUKZULLUMZCUKZDUKZULLUMZNUNZEUKZFUKZULLUMZNUNZVSWBWFNUNZNUNZU
      OGWBNUNZWFNUNZGWHNUNZUOGHNUNZWFNUNZGHWFNUNZNUNZUOWMINUNZGHINUNZNUNZUOABCD
      EFGHIMLJUBVSGUOZWGWKWIWLWTWCWJWFNVSGWBNUPUQVSGWHNUPURWBHUOZWKWNWLWPXAWJWM
      WFNWBHGNUSUQXAWHWOGNWBHWFNUPUTURWFIUOZWNWQWPWSWFIWMNUSXBWOWRGNWFIHNUSUTUR
      VQMVAVRMVAVBZVTMVAWAMVAVBZWDMVAWEMVAVBZVCQRULZLUMZSTULZLUMZWGWIQSUOZRTUOZ
      XGXIUOZUIUJXJXKVBXFXHUOXLQRSTVDXFXHLVEVFVGXCXDXEWGXGUOXCXDVBZXEVBWGOPULLU
      MZWFNUNZXGXMWGXOUOXEXMWCXNWFNUCUQVHXMOMVAPMVAVBXEXOXGUOUGUEVIVJVKXCXDXEWI
      XIUOXCXDXEVBZVBWIVSUAKULLUMZNUNZXIXPWIXRUOXCXPWHXQVSNUDUTVLXPXCUAMVAKMVAV
      BXRXIUOUHUFVMVJVPVNVO $.
      $( [4-Jun-2013] $) $( [31-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d w v u C $.  $d x y z w v u F $.
    $d x y z w v u R $.  $d x y z w v u S $.  $d x y z w v u G $.
    $d z w v u D $.
    ecovdi.1 $e |- D = ( ( S X. S ) /. R ) $.
    $( M,N for BFC $)
    ecovdi.2 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. v , u >. ] R ) = [ <. M , N >. ] R ) $.
    $( H,J for AG(BFC) $)
    ecovdi.3 $e |- ( ( ( x e. S /\ y e. S ) /\ ( M e. S /\ N e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. M , N >. ] R ) = [ <. H , J >. ] R ) $.
    $( W,X for AGB $)
    ecovdi.4 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. z , w >. ] R ) = [ <. W , X >. ] R ) $.
    $( Y,Z for AGC $)
    ecovdi.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. v , u >. ] R ) = [ <. Y , Z >. ] R ) $.
    $( K,L for (AGB)F(AGC) $)
    ecovdi.6 $e |- ( ( ( W e. S /\ X e. S ) /\ ( Y e. S /\ Z e. S ) ) ->
 ( [ <. W , X >. ] R F [ <. Y , Z >. ] R ) = [ <. K , L >. ] R ) $.
    ecovdi.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( M e. S /\ N e. S ) ) $.
    ecovdi.8 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( W e. S /\ X e. S ) ) $.
    ecovdi.9 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( Y e. S /\ Z e. S ) ) $.
    ecovdi.10 $e |- H = K $.
    ecovdi.11 $e |- J = L $.
    $( Lemma used to transfer a distributive law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovdi $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) ) $=
      ( cv cop co wceq oveq1 oveq12d eqeq12d oveq2d oveq2 oveq1d wcel wa opeq12
      cec w3a eceq2 syl mp2an adantl sylan2 eqtrd 3impb oveqan12d syl2an 3impdi
      3eqtr4a 3ecoptocl ) AUPZBUPZUQKVIZCUPZDUPZUQKVIZEUPZFUPZUQKVIZMURZNURZWEW
      HNURZWEWKNURZMURZUSGWLNURZGWHNURZGWKNURZMURZUSGHWKMURZNURZGHNURZWSMURZUSG
      HIMURZNURZXCGINURZMURZUSABCDEFGHILKJUEWEGUSZWMWQWPWTWEGWLNUTXIWNWRWOWSMWE
      GWHNUTWEGWKNUTVAVBWHHUSZWQXBWTXDXJWLXAGNWHHWKMUTVCXJWRXCWSMWHHGNVDVEVBWKI
      USZXBXFXDXHXKXAXEGNWKIHMVDVCXKWSXGXCMWKIGNVDVCVBWCLVFWDLVFVGZWFLVFWGLVFVG
      ZWILVFWJLVFVGZVJOPUQZKVIZQRUQZKVIZWMWPOQUSZPRUSZXPXRUSZUNUOXSXTVGXOXQUSYA
      OPQRVHXOXQKVKVLVMXLXMXNWMXPUSXLXMXNVGZVGWMWESTUQKVIZNURZXPYBWMYDUSXLYBWLY
      CWENUFVCVNYBXLSLVFTLVFVGYDXPUSUKUGVOVPVQXLXMXNWPXRUSXLXMVGZXLXNVGZVGWPUAU
      BUQKVIZUCUDUQKVIZMURZXRYEYFWNYGWOYHMUHUIVRYEUALVFUBLVFVGUCLVFUDLVFVGYIXRU
      SYFULUMUJVSVPVTWAWB $.
      $( [4-Jun-2013] $) $( [2-Sep-1995] $)
  $}

