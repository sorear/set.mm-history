$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite Cartesian products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c X_ $.

  $( Extend class notation to include infinite Cartesian products. $)
  cixp $a class X_ x e. A B $.

  ${
    $d f x $.  $d f A $.  $d f B $.
    $( Definition of infinite Cartesian product of [Enderton] p. 54.  Enderton
       uses a bold "X" with ` x e. A ` written underneath or as a subscript,
       as does Stoll p. 47.  Some books use a capital pi, but we will reserve
       that notation for products of numbers.  Usually ` B ` represents a
       class expression containing ` x ` free and thus can be thought of
       as ` B ( x ) ` . $)
    df-ixp $a |- X_ x e. A B =
                 { f | ( f Fn A /\ A. x e. A ( f ` x ) e. B ) } $.
  $}

  ${
    $d f A $.  $d f B $.  $d f x F $.
    $( Membership in an infinite Cartesian product.  See ~ df-ixp for
       discussion of the notation. $)
    elixp2 $p |- ( F e. X_ x e. A B <->
                ( F e. V /\ F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( vf cvv wcel cixp wa wfn cv cfv wral w3a wceq fneq1 fveq1 eleq1d
      ralbidv anbi12d df-ixp elab2g pm5.32i elisset pm4.71ri 3anass 3bitr4 ) DF
      GZDABCHZGZIUHDBJZAKZDLZCGZABMZIZIUJUHUKUONUHUJUPEKZBJZULUQLZCGZABMZIUPEDU
      IFUQDOZURUKVAUOBUQDPVBUTUNABVBUSUMCULUQDQRSTABCEUAUBUCUJUHDUIUDUEUHUKUOUF
      UG $.
      $( [29-Sep-2006] $) $( [28-Sep-2006] $)
  $}

  ${
    $d x F $.
    elixp.1 $e |- F e. V $.
    $( Membership in an infinite Cartesian product. $)
    elixp $p |- ( F e. X_ x e. A B <->
                ( F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( cvv wcel wfn cv cfv wral w3a wa cixp 3anass elixp2 biantrur 3bitr4 ) DF
      GZDBHZAIDJCGABKZLSTUAMZMDABCNGUBSTUAOABCDPSUBEQR $.
      $( [30-Sep-2006] $) $( [28-Sep-2006] $)
  $}

  ${
    $d f x A $.  $d f x B $.  $d f C $.
    $( Equality theorem for infinite Cartesian product. $)
    ixpeq1 $p |- ( A = B -> X_ x e. A C = X_ x e. B C ) $=
      ( vf wceq cv wfn cfv wcel wral wa cab cixp fneq2 raleq1 anbi12d abbidv
      df-ixp 3eqtr4g ) BCFZEGZBHZAGUBIDJZABKZLZEMUBCHZUDACKZLZEMABDNACDNUAUFUIE
      UAUCUGUEUHBCUBOUDABCPQRABDESACDEST $.
      $( [9-Jun-2007] $) $( [29-Sep-2006] $)
  $}

  ${
    $d f A $.  $d f B $.  $d f C $.  $d f x $.
    $( Subclass theorem for infinite Cartesian product. $)
    ss2ixp $p |- ( A. x e. A B (_ C -> X_ x e. A B (_ X_ x e. A C ) $=
      ( vf wss wral cixp cv wfn cfv wcel wa ssel r19.20sii anim2d visset elixp
      3imtr4g ssrdv ) CDFZABGZEABCHZABDHZUBEIZBJZAIUEKZCLZABGZMUFUGDLZABGZMUEUC
      LUEUDLUBUIUKUFUAUHUJABCDUGNOPABCUEEQZRABDUEULRST $.
      $( [17-Oct-2006] $) $( [29-Sep-2006] $)
  $}

  $( Equality theorem for infinite Cartesian product. $)
  ixpeq2 $p |- ( A. x e. A B = C -> X_ x e. A B = X_ x e. A C ) $=
    ( wss wral wa cixp wceq ss2ixp anim12i eqss ralbii r19.26 bitr 3imtr4 ) CDE
    ZABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQSGZABFUA
    UFUHABCDLMQSABNOUBUCLP $.
    $( [16-Aug-2007] $) $( [29-Sep-2006] $)

  ${
    $d f x y A $.  $d f y B $.  $d x y F $.
    $( A member of an infinite Cartesian product maps to the indexed union of
       the product argument.  Remark in [Enderton] p. 54. $)
    ixpf $p |- ( F e. X_ x e. A B -> F : A --> U_ x e. A B ) $=
      ( vy cixp wcel cvv wfn cv cfv wral w3a ciun wf elixp2 wa ssiun2 sseld
      r19.20i anim2i ax-17 hbiu1 ffnfvf sylibr 3adant1 sylbi ) DABCFGDHGZDBIZAJ
      ZDKZCGZABLZMBABCNZDOZABCDPUIUMUOUHUIUMQUIUKUNGZABLZQUOUMUQUIULUPABUJBGCUN
      UKABCRSTUAAEBUNDEJZBGAUBAEBCUCURDGAUBUDUEUFUG $.
      $( [4-Oct-2006] $) $( [28-Sep-2006] $)

    $( The union of an infinite Cartesian product is included in a cross
       product. $)
    uniixp $p |- U. X_ x e. A B (_ ( A X. U_ x e. A B ) $=
      ( vy vf cixp cuni ciun cxp cv wcel wf wa wex eluni ixpf anim2i 19.22i
      sylbi fssxp sseld impcom 19.23aiv syl ssriv ) DABCFZGZBABCHZIZDJZUGKZUJEJ
      ZKZBUHULLZMZENZUJUIKZUKUMULUFKZMZENUPEUJUFOUSUOEURUNUMABCULPQRSUOUQEUNUMU
      QUNULUIUJBUHULTUAUBUCUDUE $.
      $( [24-Oct-2006] $) $( [28-Sep-2006] $)

    $( The existence of an infinite Cartesian product.  ` x ` is normally a
       free-variable parameter in ` B ` .  Remark in Enderton p. 54. $)
    ixpexg $p |- ( ( A e. C /\ A. x e. A B e. D ) -> X_ x e. A B e. V ) $=
      ( wcel wral wa cixp cuni cvv ciun cxp iunexg xpexg syldan wss uniixp
      ssexg mpan syl uniexb sylibr ) BDFZCEFABGZHZABCIZJZKFZUGKFUFBABCLZMZKFZUI
      UDUEUJKFULABCDENBUJDKOPUHUKQULUIABCRUHUKKSTUAUGUBUC $.
      $( [28-Jun-2007] $) $( [28-Sep-2006] $)
  $}

  ${
    $d f A $.  $d f x $.
    $( An infinite Cartesian product with an empty index set. $)
    ixp0x $p |- X_ x e. (/) A = { (/) } $=
      ( vf c0 cixp cv wfn cfv wcel wral wa cab csn df-ixp wceq elsn fn0 ral0
      biantru 3bitr2 abbi2i eqtr4 ) ADBECFZDGZAFUCHBIZADJZKZCLDMZADBCNUGCUHUCUH
      IUCDOUDUGCDPUCQUFUDUEARSTUAUB $.
      $( [21-Sep-2007] $) $( [21-Sep-2007] $)
  $}

  $( Membership of the empty set in an infinite Cartesian product.
     (Contributed by Steve Rodriguez, 29-Sep-2006.) $)
  0elixp $p |- (/) e. X_ x e. (/) A $=
    ( c0 csn cixp 0ex snid ixp0x eleqtrr ) CCDACBECFGABHI $.
    $( [30-Sep-2006] $) $( [29-Sep-2006] $)

  ${
    $d f A $.  $d f B $.  $d f x $.
    $( The infinite Cartesian product of a family ` B ( x ) ` with an empty
       member is empty. $)
    ixp0 $p |- ( E. x e. A B = (/) -> X_ x e. A B = (/) ) $=
      ( vf c0 wceq wrex cixp cv wfn cfv wcel wral wa wn wb n0i con2i r19.22si
      rexnal sylib intnand noel jctir pm5.21 syl visset elixp syl5bb eqrdv ) CE
      FZABGZDABCHZEULDIZBJZAIUNKZCLZABMZNZUNELZUNUMLULUSOZUTOZNUSUTPULVAVBULURU
      OULUQOZABGUROUKVCABUQUKCUPQRSUQABTUAUBUNUCUDUSUTUEUFABCUNDUGUHUIUJ $.
      $( [1-Oct-2006] $) $( [1-Oct-2006] $)
 $}

  ${
    $d f x A $.  $d f B $.
    mapixp.1 $e |- A e. V $.
    mapixp.2 $e |- B e. V $.
    ${
      $d x B $.
      $( Express set exponentiation in terms of an infinite Cartesian product.
         Remark in [Stoll] p. 47.  Note that ` B ` is constant, i.e. ` x ` does
         not occur in ` B ` . $)
      mapixp $p |- ( B ^m A ) = X_ x e. A B $=
        ( vf cm co cixp cv wf wfn cfv wcel wral wa ffnfv elmap visset elixp
        3bitr4 eqriv ) FCBGHZABCIZBCFJZKUEBLAJUEMCNABOPUEUCNUEUDNABCUEQCBUEEDRA
        BCUEFSTUAUB $.
        $( [2-Oct-2006] $) $( [28-Sep-2006] $)
    $}

    $( An infinite Cartesian product is a subset of set exponentation.  Remark
       in [Enderton] p. 54. $)
    ixpssmap $p |- X_ x e. A B (_ ( U_ x e. A B ^m A ) $=
      ( vf cixp ciun cm co cv wcel wf ixpf iunex elmap sylibr ssriv ) FABCGZABC
      HZBIJZFKZSLBTUBMUBUALABCUBNTBUBABCDEODPQR $.
      $( [8-Jul-2007] $) $( [28-Sep-2006] $)
  $}

$(
  @{
    xpixp.1 @e |- P = X_ x e. { (/) , 1o } if ( x = (/) , A , B ) @.
    xpixp.1 @e |- P = X_ x e. 2o if ( x = (/) , A , B ) @.
    xpixp.1 @e |- P =
               X_ x e. { (/) , 1o } ( { <. (/) , A >. , <. 1o , B >. } ` x ) @.
    @( A cross product in terms of an infinite Cartesian product. @)
    xpixp @p |- ( A X. B ) =
                ( ran ( U. P |` { (/) } ) X. ran ( U. P |` { 1o } ) ) @=
?@.
      @( [?] @) @( [29-Sep-2006] @)
  @}

  @{
    ixpxp.1 @e |- P = ( A X. B ) @.
    @( An infinite Cartesian product in terms of a cross product. @)
    ixpxp @p |- X_ x e. { (/) , 1o } if ( x = (/) , A , B ) =
                { ( { (/) } X. dom P ) , ( { 1o } X. ran P ) } @=
?@.
      @( [?] @) @( [29-Sep-2006] @)
  @}
$)


