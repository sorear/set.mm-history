$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Transfinite recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d y z B $.  $d x y z w F $.  $d x y z w G $.
    $( A technical lemma for transfinite recursion.  Compare Lemma 1 of
       [TakeutiZaring] p. 47.  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 19-Jun-2012.) $)
    tfrlem1 $p |- ( A e. On -> ( ( F Fn A /\ G Fn A ) ->
                  ( A. x e. A ( ( F ` x ) = ( B ` ( F |` x ) ) /\
                                        ( G ` x ) = ( B ` ( G |` x ) ) ) ->
                  A. x e. A ( F ` x ) = ( G ` x ) ) ) ) $=
      ( vy vz vw con0 wcel wa wss cv cfv wceq wral wi raleq imbi12d syl6 imbi2d
      cres ssid sseq1 anbi2d anbi12d ralim onelss sstr2 anim2d ax-17 hbra1 ssel
      wfn ra4 syl9 r19.21ad anim12d com23 r19.21adv fveq2 eqeq12d syl5bb cbvral
      cbvralv fvreseq biimpar fveq2d eqeq12 syl5ibr exp4c com4l ralimdva syl5bi
      imp4a imp31 ex imim12d pm2.43 syl56 tfis2 vtoclga mpii ) BIJDBUNEBUNKZBBL
      ZAMZDNZDWFUBZCNZOWFENZEWFUBZCNZOKZABPZWGWJOZABPZQZBUCWDFMZBLZWMAWRPZWOAWR
      PZQZQZQWDWEWQQZQFBIWRBOZXCXDWDXEWSWEXBWQWRBBUDXEWTWNXAWPWMAWRBRWOAWRBRSSU
      AWRIJZWDWSWTXAWDWSKZWTKZXAQZWDGMZBLZKZWMAXJPZKZWOAXJPZQZFGWRXJOZXHXNXAXOX
      QXGXLWTXMXQWSXKWDWRXJBUDUEWMAWRXJRUFWOAWRXJRSXPGWRPXNGWRPZXOGWRPZQXFXHXIQ
      XIXNXOGWRUGXFXHXRXSXIXFXHXNGWRXFXJWRJZXHXNXFXTXJWRLZXHXNQWRXJUHYAXGXLWTXM
      YAWSXKWDXJWRBUIUJYAWTWMAXJYAAUKWMAWRULYAWFXJJWFWRJZWTWMXJWRWFUMWMAWRUOUPU
      QURTUSUTXFXSXGWTXAXFXGXSXBXFXGXSXBQXSHMZDNZYCENZOZHWFPZAWRPZXFXGKZXBXOYGG
      AWRWOAXJULYGGUKXOYFHXJPXJWFOYGWOYFAHXJWFYCOWGYDWJYEWFYCDVAWFYCEVAVBVEYFHX
      JWFRVCVDYIYHWMWOQZAWRPXBYIYGYJAWRXFXGYBYGYJQZXFYBXGYKXFYBWDWSYKXFYBWFWRLZ
      WDWSYKQQWRWFUHYLWSWFBLZWDYKWFWRBUIWMWDYMYGWOWMWDYMYGWOWDYMKZYGKZWOWMWIWLO
      YOWHWKCYNWHWKOYGHBWFDEVFVGVHWGWIWJWLVIVJVKVLUPTVOUSVPVMWMWOAWRUGTVNVQUSVO
      VRXHXAVSVTWAVKWBWC $.
      $( [24-Jul-2012] $) $( [23-Mar-1995] $)
  $}

  ${
    $d w A $.  $d w F $.  $d w G $.  $d w x $.
    $( Lemma for transfinite recursion.  This provides some messy details
       needed to link ~ tfrlem1 into the main proof.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Jun-2012.) $)
    tfrlem2 $p |- ( ( F Fn A /\ G Fn A ) ->
       ( ( <. x , y >. e. F /\ <. x , z >. e. G ) ->
       ( A e. On -> ( A. w ( A e. On -> ( w e. A ->
          ( ( F ` w ) = ( B ` ( F |` w ) ) /\
            ( G ` w ) = ( B ` ( G |` w ) ) ) ) ) ->
       y = z ) ) ) ) $=
      ( wfn wa cv cop wcel cfv cres wceq wi wal 19.28v imp con0 abai albii wral
      3bitr3ri df-ral anbi2i adantlr tfrlem1 com12 imp3a adantr eqeq12d sylsyld
      fnop fveq2 rcla4v adantlrr sylan2br wb fnfun anim12i visset funopfv sylan
      wfun an4s eqeq12 syl mpbid sylan2b exp43 ) GEIZHEIZJZAKZBKZLGMZVPCKZLHMZJ
      ZEUAMZWBDKZEMWCGNZGWCOFNPWCHNZHWCOFNPJZQZQZDRZVQVSPZWBWIJZVOWAJZWBWGDRZJZ
      WJWBWGJZDRWBWHJZDRWNWKWOWPDWBWGUBUCWBWGDSWBWHDSUEWLWNJVPGNZVPHNZPZWJWNWLW
      BWFDEUDZJZWSWTWMWBWFDEUFUGVOVRXAWSVTVOVRJZXAWSXBVPEMZXAWDWEPZDEUDZWSVMVRX
      CVNEVPVQGUOUHVOXAXEQVRVOWBWTXEWBVOWTXEQDEFGHUIUJUKULXDWSDVPEWCVPPWDWQWEWR
      WCVPGUPWCVPHUPUMUQUNTURUSWLWSWJUTZWNWLWQVQPZWRVSPZJZXFVOGVFZHVFZJWAXIVMXJ
      VNXKEGVAEHVAVBXJVRXKVTXIXJVRJXGXKVTJXHXJVRXGVPVQGBVCVDTXKVTXHVPVSHCVCVDTV
      BVGVEWQVQWRVSVHVIULVJVKVL $.
      $( [24-Jul-2012] $) $( [23-Mar-1995] $)
  $}

  ${
    $d x y f g $.  $d x y z $.  $d g z $.  $d f g G $.  $d x z G $.
    tfrlem3.1 $e |- A = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    $( Lemma for transfinite recursion.  Let ` A ` be the class of "acceptable"
       functions.  The final thing we're interested in is the union of all
       these acceptable functions.  This lemma just changes some bound
       variables in ` A ` for later use. $)
    tfrlem3 $p |- A = { g | E. z e. On ( g Fn z /\
                A. y e. z ( g ` y ) = ( G ` ( g |` y ) ) ) } $=
      ( cv wfn cfv cres wceq wral wa con0 wrex cab wcel anbi12d eqeq12d ralbidv
      visset fneq1 fveq1 reseq1 fveq2d rexbidv fneq2 raleq cbvrexv bitri abbi2i
      elab eqtri ) DEIZAIZJZBIZUPKZUPUSLZGKZMZBUQNZOZAPQZERZFIZCIZJZUSVHKZVHUSL
      ZGKZMZBVINZOZCPQZFRHVQFVGVHVGSVHUQJZVNBUQNZOZAPQZVQVFWAEVHFUCUPVHMZVEVTAP
      WBURVRVDVSUQUPVHUDWBVCVNBUQWBUTVKVBVMUSUPVHUEWBVAVLGUPVHUSUFUGUAUBTUHUNVT
      VPACPUQVIMVRVJVSVOUQVIVHUIVNBUQVIUJTUKULUMUO $.
      $( [3-May-2004] $) $( [9-Apr-1995] $)
  $}

  ${
    $d x y f g $.  $d f g G $.
    tfrlem3a.1 $e |- A = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    $( Lemma for transfinite recursion.  Let ` A ` be the class of "acceptable"
       functions.  The final thing we're interested in is the union of all
       these acceptable functions.  This lemma just changes some bound
       variables in ` A ` for later use. $)
    tfrlem3a $p |- A = { g | E. x e. On ( g Fn x /\
                A. y e. x ( g ` y ) = ( G ` ( g |` y ) ) ) } $=
      ( cv wfn cfv cres wceq wral wa con0 wrex cab visset fneq1 fveq1 eqeq12d
      reseq1 fveq2d ralbidv anbi12d rexbidv elab abbi2i eqtri ) CDHZAHZIZBHZUJJ
      ZUJUMKZFJZLZBUKMZNZAOPZDQZEHZUKIZUMVBJZVBUMKZFJZLZBUKMZNZAOPZEQGVJEVAUTVJ
      DVBERUJVBLZUSVIAOVKULVCURVHUKUJVBSVKUQVGBUKVKUNVDUPVFUMUJVBTVKUOVEFUJVBUM
      UBUCUAUDUEUFUGUHUI $.
      $( [22-Jul-2012] $)
  $}

  ${
    $d f x y g z G $.
    tfrlem.1 $e |- A = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    $( Lemma for transfinite recursion. ` A ` is the class of all "acceptable"
       functions, and ` F ` is their union.  First we show that an acceptable
       function is in fact a function. $)
    tfrlem4 $p |- ( g e. A -> Fun g ) $=
      ( vz cv wcel wfn cfv cres wceq wral wa con0 wrex wfun tfrlem3 fnfun sylbi
      abeq2i adantr r19.23aivw ) EIZCJUFHIZKZBIZUFLUFUIMFLNBUGOZPZHQRZUFSZULECA
      BHCDEFGTUCUKUMHQUHUMUJUGUFUAUDUEUB $.
      $( [3-May-2004] $) $( [9-Apr-1995] $)

    $d h w G $.  $d f g h u v w x y z $.
    $( Lemma for transfinite recursion.  The values of two acceptable functions
       are the same within their domains. $)
    tfrlem5 $p |- ( ( g e. A /\ h e. A ) ->
                  ( ( <. x , u >. e. g /\ <. x , v >. e. h ) -> u = v ) ) $=
      ( vz vw cv wcel wa cfv cres wceq con0 wi wfn wral wrex cop tfrlem3 abeq2i
      anbi12i reeanv bitr4i cin wal onin r19.26m simpr elin sylib prth syl5 wss
      wb onelss impac fvres resabs1 fveq2d eqeqan12rd anbi12d syl bicomd mpbidi
      exp3a sylbir anim2i an4s 2elresin fnresin1 fnresin2 tfrlem2 syl2an sylbid
      alimi com24 com3r imp32 ex r19.23aivv sylbi ) GMZENZHMZENZOZWHKMZUAZBMZWH
      PZWHWOQZIPZRZBWMUBZOZWJLMZUAZWOWJPZWJWOQZIPZRZBXBUBZOZOZLSUCKSUCZAMZDMZUD
      ZWHNXLCMZUDZWJNOZXMXORZTZWLXAKSUCZXILSUCZOXKWIXTWKYAXTGEABKEFGIJUEUFYAHEA
      BLEFHIJUEUFUGXAXIKLSSUHUIXJXSKLSSWMSNXBSNOZXJXSYBWMXBUJZSNZWNXCOZYDWOYCNZ
      WOWHYCQZPZYGWOQZIPZRZWOWJYCQZPZYLWOQZIPZRZOZTTZBUKZOZXSXJWMXBULWNXCWTXHYT
      WTXHOZYSYEUUAWOWMNZWSTWOXBNZXGTOZBUKYSWSXGBWMXBUMUUDYRBUUDYDYFYQYDYFOZWSX
      GOZYQUUDUUEUUBUUCOZUUDUUFUUEYFUUGYDYFUNWOWMXBUOUPUUBWSUUCXGUQURUUEYQUUFUU
      EWOYCUSZYFOZYQUUFUTYDYFUUHYCWOVAVBUUIYKWSYPXGYFUUHYHWPYJWRWOYCWHVCUUHYIWQ
      IWHWOYCVDVEVFYFUUHYMXDYOXFWOYCWJVCUUHYNXEIWJWOYCVDVEVFVGVHVIVJVKWAVLVMVNY
      DYEYSXSYEYSYDXSYEXQYDYSXRYEXQXNYGNXPYLNOZYDYSXRTTZADCWMXBWHWJVOWNYGYCUAYL
      YCUAUUJUUKTXCWMXBWHVPXBWMWJVQADCBYCIYGYLVRVSVTWBWCWDVSWEWFWG $.
      $( [3-May-2004] $) $( [9-Apr-1995] $)

    $d g A $.
    tfrlem.2 $e |- F = U. A $.
    $( Lemma for transfinite recursion.  The union of all acceptable functions
       is a relation. $)
    tfrlem6 $p |- Rel F $=
      ( vg cuni wceq wrel cv reluni wcel wfun tfrlem4 funrel syl mprgbir mpbiri
      releq ax-mp ) ECJZKZELZHUEUFUDLZUGIMZLZICICNUHCOUHPUIABCDIFGQUHRSTEUDUBUA
      UC $.
      $( [3-May-2004] $) $( [8-Aug-1994] $)

    $d h A $.  $d u v x F $.
    $( Lemma for transfinite recursion.  The union of all acceptable functions
       is a function. $)
    tfrlem7 $p |- Fun F $=
      ( vu vv vg vh cv cop wcel wa wal wex eleq2i bitri wfun wrel wceq wi eluni
      dffun4 tfrlem6 anbi12i eeanv bitr4i an4 ancom tfrlem5 imp sylbi 19.23aivv
      cuni ax-gen gen2 mpbir2an ) EUAEUBAMZIMZNZEOZVAJMZNZEOZPZVBVEUCZUDZJQZIQA
      QAIJEUFABCDEFGHUGVKAIVJJVHVCKMZOZVLCOZPZVFLMZOZVPCOZPZPZLRKRZVIVHVOKRZVSL
      RZPWAVDWBVGWCVDVCCUQZOWBEWDVCHSKVCCUETVGVFWDOWCEWDVFHSLVFCUETUHVOVSKLUIUJ
      VTVIKLVTVNVRPZVMVQPZPZVIVTWFWEPWGVMVNVQVRUKWFWEULTWEWFVIABJICDKLFGUMUNUOU
      PUOURUSUT $.
      $( [4-May-2004] $) $( [9-Aug-1994] $)

    $d x f A $.
    $( Lemma for transfinite recursion.  The domain of ` F ` is ordinal.  (The
       proof was shortened by Alan Sare, 11-Mar-2008.) $)
    tfrlem8 $p |- Ord dom F $=
      ( cdm word cv wceq wrex cuni con0 wcel wa cfv eleq1a imp cab wss wfn cres
      wex wral abeq2i simpl reximi sylbi df-rex sylib sylan2 19.23aiv syl sylan
      fndm r19.23aiva abssi ssorduni ax-mp ciun dmeqi visset dmex dfiun2 3eqtri
      wb dmuni ordeq mpbir ) EIZJZAKZDKZIZLZDCMZAUAZNZJZVSOUBWAVRAOVQVNOPZDCVOC
      PZVPOPZVQWBWCWBVOVNUCZQZAUEZWDWCWEAOMZWGWCWEBKZVORVOWIUDFRLBVNUFZQZAOMZWH
      WLDCGUGWKWEAOWEWJUHUIUJWEAOUKULWFWDAWEWBVPVNLZWDVNVOUQWBWMWDVNOVPSTUMUNUO
      WDVQWBVPOVNSTUPURUSVSUTVAVLVTLVMWAVHVLCNZIDCVPVBVTEWNHVCDCVIDACVPVODVDVEV
      FVGVLVTVJVAVK $.
      $( [12-Mar-2008] $) $( [14-Aug-1994] $)

    $d f z F $.
    $( Lemma for transfinite recursion.  Here we compute the value of ` F `
       (the union of all acceptable functions). $)
    tfrlem9 $p |- ( y e. dom F -> ( F ` y ) = ( G ` ( F |` y ) ) ) $=
      ( vz cv cdm wcel wex cfv cres wceq wa con0 wi com3l cop visset eldm2 wral
      wfn wrex cab cuni unieqi eqtri eleq2i eluniab bitri fnop wss ra4e elssuni
      abeq2i syl6sseqr sylbir syl com12 fndm eleq2d wfun tfrlem7 funssfv mp3an1
      ra4 adantrl eleq1d onelss syl6bir imp31 w3a fun2ssres fveq2d sylan2 exp31
      eqeq12d exbiri com34 com24 sylbird syld imp4d mpdi exp4d ex com4r pm2.43i
      imp4a r19.23adv imp 19.23aiv sylbi ) BJZEKLWQIJZUAZELZIMWQENZEWQOZFNZPZIW
      QEBUBUCWTXDIWTWSDJZLZXEAJZUEZWQXENZXEWQOZFNZPZBXGUDZQZARUFZQZDMZXDWTWSXOD
      UGZUHZLXQEXSWSECUHZXSHCXRGUIUJUKXODWSULUMXPXDDXFXOXDXFXNXDARXFXGRLZXHXMXD
      XHXFYAXMXDSZXHXFYAYBSSXHXFYAXHYBXHXFYAXHYBSSXHXFQZYAXHXMXDYCWQXGLZYAXNQZX
      DSXGWQWRXEUNYDYEXEEUOZXDYEXOYFXNARUPXOXECLZYFXODCGURYGXEXTEXECUQHUSUTVAYD
      YAXHXMYFXDSZYDXMXHYAYHYDXMXLXHYAYHSZSXMYDXLXLBXGVIVBXHYDXLYIXHYDWQXEKZLZX
      LYISXHYJXGWQXGXEVCZVDXHYAXLYKYHXHYAYKXLYHXHYAYKXLYHSYFXHYAQZYKQZXLXDYFYNX
      DXLYFYNQXAXIXCXKYFYKXAXIPZYMEVEZYFYKYOABCDEFGHVFZWQEXEVGVHVJYNYFWQYJUOZXC
      XKPZXHYAYKYRXHYAYJRLYKYRSXHYJXGRYLVKYJWQVLVMVNYPYFYRYSYQYPYFYRVOXBXJFWQEX
      EVPVQVHVRVTWATVSWBWCWDTWEWCWFWGVAWHWIWJWKTWLWMWNWOWPWOWP $.
      $( [4-May-2004] $) $( [17-Aug-1994] $)

    ${
      $d w F $.
      tfrlem.3 $e |- C = ( F u. { <. dom F , ( G ` ( F |` dom F ) ) >. } ) $.
      $( Lemma for transfinite recursion.  We define class ` C ` by extending
         ` F ` with one ordered pair.  We will assume, falsely, that domain of
         ` F ` is a member of, and thus not equal to, ` On ` .  Using this
         assumption we will prove facts about ` C ` that will lead to a
         contradiction in ~ tfrlem13 , thus showing the domain of ` F ` does in
         fact equal ` On ` .  Here we show (under the false assumption) that
         ` C ` is a function extending the domain of ` F ` by one.  (The proof
         was shortened by Alan Sare, 20-Feb-2008.) $)
      tfrlem10 $p |- ( dom F e. On -> C Fn suc dom F ) $=
        ( cdm con0 csn cun wfn wfun wceq wa cin c0 wcel cres cfv funsng tfrlem7
        csuc jctil dmsnop ineq2i word tfrlem8 orddisj ax-mp eqtri funun sylancl
        cop uneq2i dmun df-suc 3eqtr4i jctir df-fn sylibr fneq1i ) FKZLUAZFVFFV
        FUBGUCZUQMZNZVFUFZOZDVKOVGVJPZVJKZVKQZRVLVGVMVOVGFPZVIPZRVFVIKZSZTQVMVG
        VQVPVFVHLUDABCEFGHIUEUGVSVFVFMZSZTVRVTVFVFVHUHZUIVFUJWATQABCEFGHIUKVFUL
        UMUNFVIUOUPVFVRNVFVTNVNVKVRVTVFWBURFVIUSVFUTVAVBVJVKVCVDVKDVJJVEVD $.
        $( [20-Feb-2008] $) $( [14-Aug-1994] $)

      $( Lemma for transfinite recursion.  Compute the value of ` C ` . $)
      tfrlem11 $p |- ( dom F e. On ->
                     ( y e. suc dom F -> ( C ` y ) = ( G ` ( C |` y ) ) ) ) $=
        ( wcel wceq cfv cres wi wa syl wss cop csn cv cdm csuc con0 elsuci wfun
        wo wfn tfrlem10 fnfun cun ssun1 sseqtr4i tfrlem9 funssfv adantrl onelss
        3expa imp fun2ssres fveq2d sylan2 eqeq12d syl5ibr sylan pm2.43i pm2.43d
        mpanl2 exp32 opeq1 adantl eqimss mp3an2 syl2an reseq2 eqtrd fveq2 opeq2
        opex snid 3syl sneqd syl5eleq elun2 syl6eleqr wb visset eqelsuc fnopfvb
        fvex mpbird ex jaod syl5 ) BUAZFUBZUCZKZWOWPKZWOWPLZUGWPUDKZWODMZDWONZG
        MZLZWOWPUEXAWSXEWTXAWSXEXAWSWSXEOZOXAXAWSXFXADUFZXAWSPZXFXADWQUHZXGABCD
        EFGHIJUIZWQDUJQZXGFDRZXHXFFFWPFWPNZGMZSZTZUKZDFXPULJUMZWSXEXGXLPZXHPZWO
        FMZFWONZGMZLABCEFGHIUNXTXBYAXDYCXSWSXBYALZXAXGXLWSYDWODFUOURUPXHXSWOWPR
        ZXDYCLXAWSYEWPWOUQUSXSYEPXCYBGXGXLYEXCYBLZWODFUTZURVAVBVCVDVHVEVIVFVGXA
        WTXEXAWTPZXEWOXDSZDKZYHYIXQDYHYIXPKYIXQKYHYIYITXPYIWOXDVSVTYHYIXOYHYIWP
        XDSZXOWTYIYKLXAWOWPXDVJVKYHXCXMLXDXNLYKXOLYHXCYBXMXAXGYEYFWTXKWOWPVLXGX
        LYEYFXRYGVMVNWTYBXMLXAWOWPFVOVKVPXCXMGVQXDXNWPVRWAVPWBWCYIXPFWDQJWEXAXI
        WRXEYJWFWTXJWOWPBWGWHWQWOXDDXCGWJWIVNWKWLWMWN $.
        $( [4-May-2004] $) $( [18-Aug-1994] $)

      $d f x y C $.  $d y F $.
      $( Lemma for transfinite recursion.  Show ` C ` is an acceptable
         function. $)
      tfrlem12 $p |- ( dom F e. On -> C e. A ) $=
        ( con0 wcel cv wfn cfv cres wceq wral wa wrex csuc suceloni fneq2 raleq
        cdm tfrlem10 tfrlem11 r19.21aiv anbi12d rcla4ev syl12anc cvv fnex fneq1
        syl11anc fveq1 reseq1 fveq2d eqeq12d ralbidv rexbidv elab2g syl mpbird
        wb ) FUEZKLZDCLZDAMZNZBMZDOZDVKPZGOZQZBVIRZSZAKTZVGVFUAZKLZDVSNZVOBVSRZ
        VRVFUBZABCDEFGHIJUFZVGVOBVSABCDEFGHIJUGUHVQWAWBSAVSKVIVSQVJWAVPWBVIVSDU
        CVOBVIVSUDUIUJUKVGDULLZVHVRVEVGWAVTWEWDWCVSKDUMUOEMZVINZVKWFOZWFVKPZGOZ
        QZBVIRZSZAKTVREDCULWFDQZWMVQAKWNWGVJWLVPVIWFDUNWNWKVOBVIWNWHVLWJVNVKWFD
        UPWNWIVMGWFDVKUQURUSUTUIVAHVBVCVD $.
        $( [4-May-2004] $) $( [15-Aug-1994] $)

      $( Lemma for transfinite recursion.  If ` dom F ` is in ` On ` , then
         ` C ` is acceptable, and thus a subset of ` F ` , but ` dom C ` is
         bigger than ` dom F ` .  This is a contradiction, so ` dom F ` must be
         ` On ` . $)
      tfrlem13 $p |- dom F = On $=
        ( cdm con0 wcel wn wceq wo word tfrlem8 ordirr wss tfrlem12 csuc sucidg
        wfn tfrlem10 fndm syl eleqtrrd wi cuni elssuni syl6sseqr dmss ssel 3syl
        sylc nsyl ax-mp ordeleqon mpbi orel1 mp2 ) FKZLMZNZVDVCLOZPZVFVCQZVEABC
        EFGHIRZVHVCVCMZVDVCSVDDCMZVCDKZMZVJABCDEFGHIJUAVDVCVCUBZVLVCLUCVDDVNUDV
        LVNOABCDEFGHIJUEVNDUFUGUHVKDFTVLVCTVMVJUIVKDCUJFDCUKIULDFUMVLVCVCUNUOUP
        UQURVHVGVIVCUSUTVDVFVAVB $.
        $( [4-May-2004] $) $( [14-Aug-1994] $)

    $}
  $}

  ${
    $d f x A $.  $d x y f F $.  $d x y f G $.
    tfr.1 $e |- A = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    tfr.2 $e |- F = U. A $.
    $( TODO - rewrite so ~ eqid can eliminate hyps. $)
    $( Principle of Transfinite Recursion, part 1 of 3.  Theorem 7.41(1) of
       [TakeutiZaring] p. 47.  We start with an arbitrary class ` G ` ,
       normally a function, and define a class ` A ` of all "acceptable"
       functions.  The final function we're interested in is the union ` F ` of
       them. ` F ` is then said to be defined by transfinite recursion.  The
       purpose of the 3 parts of this theorem is to demonstrate properties of
       ` F ` .  In this first part we show that ` F ` is a function whose
       domain is all ordinal numbers. $)
    tfr1 $p |- F Fn On $=
      ( con0 wfn wfun cdm wceq df-fn tfrlem7 cres cfv cop csn cun eqid tfrlem13
      mpbir2an ) EIJEKELZIMEINABCDEFGHOABCEUDEUDPFQRSTZDEFGHUEUAUBUC $.
      $( [5-May-2004] $) $( [17-Aug-1994] $)

    $d y z $.
    $( Principle of Transfinite Recursion, part 2 of 3.  Theorem 7.41(2) of
       [TakeutiZaring] p. 47.  Here we show that the function ` F ` has the
       property that for any function ` G ` whatsoever, the "next" value of
       ` F ` is ` G ` recursively applied to all "previous" values of
       ` F ` . $)
    tfr2 $p |- ( z e. On -> ( F ` z ) = ( G ` ( F |` z ) ) ) $=
      ( cv cfv cres wceq con0 fveq2 reseq2 fveq2d eqeq12d wcel cdm cop csn eqid
      cun tfrlem13 eleq2i tfrlem9 sylbir vtoclga ) BJZFKZFUJLZGKZMZCJZFKZFUOLZG
      KZMBUONUJUOMZUKUPUMURUJUOFOUSULUQGUJUOFPQRUJNSUJFTZSUNUTNUJABDFUTFUTLGKUA
      UBUDZEFGHIVAUCUEUFABDEFGHIUGUHUI $.
      $( [5-May-2004] $) $( [9-Apr-1995] $)

    $d x y B $.
    $( Principle of Transfinite Recursion, part 3 of 3.  Theorem 7.41(3) of
       [TakeutiZaring] p. 47.  Finally we show that ` F ` is unique.  We do
       this by showing that any class ` B ` with the same properties of ` F `
       that we showed in parts 1 and 2 is identical to ` F ` . $)
    tfr3 $p |- ( ( B Fn On /\
               A. x e. On ( B ` x ) = ( G ` ( B |` x ) ) ) -> B = F ) $=
      ( con0 wfn cv cfv cres wceq wral wa wi fveq2 wb ax-17 hban eqeq12d imbi2d
      hbra1 wcel hbim r19.21v ra4 wss onss fvreseq mpanl2 syl6bir sylan2 ancoms
      tfr1 adantr tfr2 jctr jcab sylibr eqeq12 syl6 adantl mpbird exp43 pm2.43d
      imp com4t exp4a syl com3l imp3a syl5bi tfis2f com12 r19.21ai eqfnfv mpan2
      a2d eqid biimpar mpanr1 syldan ) DJKZALZDMZDWGNZGMZOZAJPZWHWGFMZOZAJPZDFO
      ZWFWLQZWNAJWFWLAWFAUAWKAJUEUBZWGJUFZWQWNWQWNRZWQBLZDMZXAFMZOZRZABWQXDAWRX
      DAUAUGWGXAOZWNXDWQXFWHXBWMXCWGXADSWGXAFSUCUDXEBWGPWQXDBWGPZRWSWTWQXDBWGUH
      WSWQXGWNWSWFWLXGWNRZWLWSWFXHWLWSWKRZWSWFXHRZRWKAJUIXIWSXJXIWSWSWFXHWSWFQZ
      XGXIWSWNXKXGXIWSWNXKXGQZXIWSQZQWNWJFWGNZGMZOZXLXPXMXKXGXPWFWSXGXPRZWSWFWG
      JUJZXQWGUKWFXRQXGWIXNOZXPWFFJKZXRXSXGTABCEFGHIUQZBJWGDFULUMWIXNGSUNUOUPVI
      URXMWNXPTZXLXIWSYBXIWSWKWMXOOZQZYBXIXIWSYCRZQWSYDRXIYEABACEFGHIUSUTWSWKYC
      VAVBWHWJWMXOVCVDVIVEVFVGVJVKVHVLVMVNWAVOVPVQVRWFJJOZWOWPJWBWFWPYFWOQZWFXT
      WPYGTYAAJJDFVSVTWCWDWE $.
      $( [5-May-2004] $) $( [18-Aug-1994] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y H $.
    tz7.44lem1.1 $e |- G = { <. x , y >. | ( ( x = (/) /\ y = A ) \/
            ( -. ( x = (/) \/ Lim dom x ) /\ y = ( H ` ( x ` U. dom x ) ) )
                \/ ( Lim dom x /\ y = U. ran x ) ) } $.
    $( ` G ` is a function.  Lemma for ~ tz7.44-1 , ~ tz7.44-2 , and
       ~ tz7.44-3 .  (Unnecessary distinct variable restrictions were removed
       by David Abernethy, 19-Jun-2012.) $)
    tz7.44lem1 $p |- Fun G $=
      ( wfun cv c0 wceq wa cdm wlim cuni cfv cvv wcel syl ax-mp wb wo crn copab
      wn w3o wmo funopab fvex visset rnexg uniexg nlim0 limeq mtbir dmeq biimpa
      dm0 mto moeq3 mpgbir funeq mpbir ) DGZAHZIJZBHZCJKVEVDLZMZUAUDVFVGNVDOZEO
      ZJKVHVFVDUBZNZJKUEZABUCZGZVOVMBUFAVMABUGVEVHBCVJVLVIEUHVDPQZVLPQZAUIVPVKP
      QVQVDPUJVKPUKRSVEVHKILZMZVSIMZULVRIJVSVTTUQVRIUMSUNVEVHVSVEVGVRJVHVSTVDIU
      OVGVRUMRUPURUSUTDVNJVCVOTFDVNVASVB $.
      $( [24-Jul-2012] $) $( [23-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x y F $.  $d y G $.  $d x H $.
    tz7.44.1 $e |- G = ( x e. _V |-> if ( x = (/) , A ,
                if ( Lim dom x , U. ran x , ( H ` ( x ` U. dom x ) ) ) ) ) $.
    tz7.44.2 $e |- F Fn On $.
    tz7.44.3 $e |- ( y e. On -> ( F ` y ) = ( G ` ( F |` y ) ) ) $.
    ${
      $d x y A $.
      tz7.44.4 $e |- A e. _V $.
      $( The value of ` F ` at ` (/) ` .  Part 1 of Theorem 7.44 of
         [TakeutiZaring] p. 49. $)
      tz7.44-1 $p |- ( F ` (/) ) = A $=
        ( c0 cfv cres con0 wcel wceq cv ax-mp cvv cuni 0elon reseq2 fveq2d res0
        fveq2 eqeq12d vtoclga fveq2i 0ex cdm wlim crn cif iftrue fvmpt 3eqtri )
        KDLZDKMZELZKELZCKNOUQUSPZUABQZDLZDVBMZELZPVABKNVBKPZVCUQVEUSVBKDUEVFVDU
        REVBKDUBUCUFIUGRURKEDUDUHKSOUTCPUIAKAQZKPZCVGUJZUKVGULTVITVGLFLUMZUMCSE
        VHCVJUNGJUORUP $.
        $( [8-Sep-2013] $) $( [23-Apr-1995] $)
    $}

    $( The value of ` F ` at a successor ordinal.  Part 2 of Theorem 7.44 of
       [TakeutiZaring] p. 49.  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 19-Jun-2012.)  (The proof was shortened by
       Mario Carneiro, 24-Jul-2012.) $)
    tz7.44-2 $p |- ( B e. On -> ( F ` suc B ) = ( H ` ( F ` B ) ) ) $=
      ( con0 wcel cfv wceq fveq2d syl c0 cuni cvv eqtrd csuc suceloni cv reseq2
      cres fveq2 eqeq12d vtoclga cdm wlim crn cif fnfun ax-mp resfunexg sylancr
      wfun wfn wn wne nsuceq0 cin wss onss dfss sylib dmres fndm eqtri syl6reqr
      ineq2i eqeq1d necon3bbid mpbiri dmeq dm0 syl6eq iffalse nlimsucg wb limeq
      nsyl mtbird unieqd word eloni ordunisuc sucidg fvres 3eqtrd fvex syl6eqel
      eqeq1 eqidd rneq fveq1 ifbieq12d fvmptg syl11anc ) DKLZDUAZEMZEXAUEZFMZDE
      MZGMZWTXAKLZXBXDNZDUBZBUCZEMZEXJUEZFMZNXHBXAKXJXANZXKXBXMXDXJXAEUFXNXLXCF
      XJXAEUDOUGJUHPWTXDXCQNZCXCUIZUJZXCUKZRZXPRZXCMZGMZULZULZXFWTXCSLZYDSLXDYD
      NWTEUQZXGYEEKURZYFIKEUMUNXIEXAKUOUPWTYDXFSWTYDYCYBXFWTXOUSYDYCNWTXPQNZXOW
      TYHUSXAQUTDVAWTYHXAQWTXPXAQWTXAXAKVBZXPWTXAKVCZXAYINWTXGYJXIXAVDPXAKVEVFX
      PXAEUIZVBYIEXAVGYKKXAYGYKKNIKEVHUNVKVIVJZVLVMVNXOXPQUIQXCQVOVPVQWBXOCYCVR
      PWTXQUSYCYBNWTXQXAUJZDKVSWTXPXANXQYMVTYLXPXAWAPWCXQXSYBVRPWTYAXEGWTYADXCM
      ZXEWTXTDXCWTXTXARZDWTXPXAYLWDWTDWEYODNDWFDWGPTOWTDXALYNXENDKWHDXAEWIPTOWJ
      ZXEGWKWLAXCAUCZQNZCYQUIZUJZYQUKZRZYSRZYQMZGMZULZULYDSSFYQXCNZYRXOCUUFCYCY
      QXCQWMUUGCWNUUGYTXQUUBUUEXSYBUUGYSXPNYTXQVTYQXCVOZYSXPWAPUUGUUAXRYQXCWOWD
      UUGUUDYAGUUGUUDUUCXCMYAUUCYQXCWPUUGUUCXTXCUUGYSXPUUHWDOTOWQWQHWRWSYPTT $.
      $( [8-Sep-2013] $) $( [23-Apr-1995] $)

    $( The value of ` F ` at a limit ordinal.  Part 3 of Theorem 7.44 of
       [TakeutiZaring] p. 49.  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 19-Jun-2012.) $)
    tz7.44-3 $p |- ( ( B e. V /\ Lim B ) -> ( F ` B ) = U. ( F " B ) ) $=
      ( wcel wlim cfv cuni c0 wceq con0 syl cvv wa cres crn cima cdm limelon cv
      cif fveq2 reseq2 fveq2d eqeq12d vtoclga wfn fnfun ax-mp resfunexg sylancr
      wfun wn simpr nlim0 wb cin wss onss dfss sylib dmres fndm ineq2i syl6reqr
      eqtri dmeq dm0 syl6eq sylan9req limeq mtbiri ex mpd iffalse mpbird iftrue
      con2d eqtrd rnexg uniexg 3syl eqeltrd eqeq1 eqidd unieqd ifbieq12d fvmptg
      rneq fveq1 syl11anc 3eqtrd df-ima unieqi syl6eqr ) DHLZDMZUAZDENZEDUBZUCZ
      OZEDUDZOXEXFXGFNZXGPQZCXGUEZMZXIXMOZXGNZGNZUHZUHZXIXEDRLZXFXKQZDHUFZBUGZE
      NZEYCUBZFNZQYABDRYCDQZYDXFYFXKYCDEUIYGYEXGFYCDEUJUKULKUMSXEXGTLZXSTLXKXSQ
      XEEUSZXTYHERUNZYIJREUOUPYBEDRUQURZXEXSXITXEXSXRXIXEXLUTZXSXRQXEXDYLXCXDVA
      ZXEXLXDXEXLXDUTXEXLUAZXDPMZVBYNDPQXDYOVCXEXLDXMPXEDDRVDZXMXEDRVEZDYPQXEXT
      YQYBDVFSDRVGVHXMDEUEZVDYPEDVIYRRDYJYRRQJREVJUPVKVMVLZXLXMPUEPXGPVNVOVPVQD
      PVRSVSVTWEWAXLCXRWBSXEXNXRXIQXEXNXDYMXEXMDQXNXDVCYSXMDVRSWCXNXIXQWDSWFZXE
      YHXHTLXITLYKXGTWGXHTWHWIWJAXGAUGZPQZCUUAUEZMZUUAUCZOZUUCOZUUANZGNZUHZUHXS
      TTFUUAXGQZUUBXLCUUJCXRUUAXGPWKUUKCWLUUKUUDXNUUFUUIXIXQUUKUUCXMQUUDXNVCUUA
      XGVNZUUCXMVRSUUKUUEXHUUAXGWPWMUUKUUHXPGUUKUUHUUGXGNXPUUGUUAXGWQUUKUUGXOXG
      UUKUUCXMUULWMUKWFUKWNWNIWOWRYTWSXJXHEDWTXAXB $.
      $( [24-Jul-2012] $) $( [23-Apr-1995] $)
  $}

