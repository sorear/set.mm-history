$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Finite induction (for finite ordinals)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    find.1 $e |- ( A (_ om /\ (/) e. A /\ A. x e. A suc x e. A ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of [TakeutiZaring] p. 43.  The simpler hypothesis shown here was
       suggested in an email from "Colin" on 1-Oct-01.  The hypothesis states
       that ` A ` is a set of natural numbers, zero belongs to ` A ` , and
       given any member of ` A ` the member's successor also belongs to ` A ` .
       The conclusion is that every natural number is in ` A ` . $)
    find $p |- A = om $=
      ( com wss c0 wcel cv csuc wral 3simp1i wi w3a wa ax-1 r19.20si ralcom3
      sylib anim2i 3anass 3imtr4 ax-mp peano5 3adant1 eqssi ) BDBDEZFBGZAHZIBGZ
      ABJZCKUFUGUHBGUILADJZMZDBEZUFUGUJMZULCUFUGUJNZNUFUGUKNZNUNULUOUPUFUJUKUGU
      JUHDGZUILZABJUKUIURABUIUQOPUIABDQRSSUFUGUJTUFUGUKTUAUBUGUKUMUFABUCUDUBUE
      $.
      $( [5-Mar-2004] $) $( [22-Feb-2004] $)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    finds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    finds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    finds.5 $e |- ps $.
    $( Induction hypothesis. $)
    finds.6 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136. $)
    finds $p |- ( A e. om -> ta ) $=
      ( com wcel cab c0 cv csuc wi wral wss 0ex elab mpbir visset sucex
      3imtr4g rgen peano5 mp2an sseli elabg mpbid ) HOPHAFQZPEOUPHRUPPZGSZUPPZU
      RTZUPPZUAZGOUBOUPUCUQBMABFRUDIUEUFVBGOUROPCDUSVANACFURGUGZJUEADFUTURVCUHK
      UEUIUJGUPUKULUMAEFHOLUNUO $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsg.5 $e |- ( B e. om -> ps ) $.
    $( Induction hypothesis. $)
    findsg.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.  The
       basis of this version is an arbitrary natural number ` B ` instead of
       zero. $)
    findsg $p |- ( ( ( A e. om /\ B e. om ) /\ B (_ A ) -> ta ) $=
      ( com wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl eqeq2 syl6bir imp
      imbi12d wn imbi1d ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61ian imbi2d
      a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi eqcoms
      imim2i com4r con0 onsssuc onelpsst suceloni sylan2 bitrd nnont syl2an
      ancoms ex ax-1 syl8 a2d com23 sylbird annim syl5ibr pm2.61d finds imp31
      ) HPQIPQZIHRZEWNIFSZRZATZTWNIUARZBTZTWNIGSZRZCTZTWNIXAUBZRZDTZTWNWOETZTFG
      HWPUAUCZWRWTWNIUAUCZXHWRWTUDXIXHUEWQWSABXHWQWSUDXIWPUAIUFZUGXIXHABUDZXIXH
      WPIUCZXKIUAWPUHJUIUJUKXHWRWSATXIULZWTXHWQWSAXJUMXMWSABXMWSXKWSXIIUNUOUPUQ
      URUSUTWPXAUCZWRXCWNXNWQXBACWPXAIUFKUKUTWPXDUCZWRXFWNXOWQXEADWPXDIUFLUKUTW
      PHUCZWRXGWNXPWQWOAEWPHIUFMUKUTWNBWSNVAXAPQZWNXCXFXQWNXCXFTZXQWNUEZXEIXDUC
      ZTZXRWNYAXRTXQYAXCXEWNDYAXEWNDTZTXCXTYBXEYBXDIXDIUCXOXLUEZFVBYBFXDIXAGVCV
      DVEYCYBFXLWNAXODXLABWNJNVFXOADLVGVHVIVJVKVLVAVMUGXSXEXTULUEZXRYAULXSYDXBX
      RWNXQXBYDUDZIVNQZXAVNQZYEWNXQYFYGUEXBIXDQZYDIXAVOYFXDVNQYHYDUDYGIXDVPXAVQ
      VRVSIVTXAVTWAWBXSXCXBXFXSXBCXFXSXBCDXFXSXBCDTOWCDXEWDWEWFWGWHXEXTWIWJWKWC
      WFWLWM $.
      $( [16-Sep-1995] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    finds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis. $)
    finds2.5 $e |- ( y e. om -> ( ta -> ( ch -> th ) ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136.  $)
    finds2 $p |- ( x e. om -> ( ta -> ph ) ) $=
      ( cv com wcel wi cab c0 csuc wral wss 0ex wceq imbi2d elab mpbir a2d
      visset sucex 3imtr4g rgen peano5 mp2an sseli abid sylib ) FMZNOUQEAPZFQZO
      URNUSUQRUSOZGMZUSOZVASZUSOZPZGNTNUSUAUTEBPZKURVFFRUBUQRUCABEHUDUEUFVEGNVA
      NOZECPZEDPZVBVDVGECDLUGURVHFVAGUHZUQVAUCACEIUDUEURVIFVCVAVJUIUQVCUCADEJUD
      UEUJUKGUSULUMUNURFUOUP $.
      $( [29-Nov-2002] $)
  $}

  ${
    $d x y $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    finds1.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds1.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds1.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds1.4 $e |- ps $.
    $( Induction hypothesis. $)
    finds1.5 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136.  $)
    finds1 $p |- ( x e. om -> ph ) $=
      ( cv com wcel c0 wceq eqid a1i wi a1d finds2 mpi ) ELMNOOPZAOQABCDUCEFGHI
      BUCJRFLMNCDSUCKTUAUB $.
      $( [23-Mar-2006] $) $( [22-Mar-2006] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    findes.1 $e |- [ (/) / x ] ph $.
    findes.2 $e |- ( x e. om -> ( ph -> [ suc x / x ] ph ) ) $.
    $( Finite induction with explicit substitution.  The first hypothesis is
       the basis and the second is the induction hypothesis.  Theorem Schema
       22 of [Suppes] p. 136. (Contributed by Raph Levien, 9-Jul-03.) $)
    findes $p |- ( x e. om -> ph ) $=
      ( vz vy cv wsbc c0 csuc dfsbcq sbequ sbequ12r com wcel wi ax-17 hbs1
      visset sucex hbsbc1v hbim wceq eleq1 sbequ12 wb suceq syl imbi12d chvar
      finds ) ABEGZHABIHABFGZHZABUMJZHZAEFBGZABULIKAEFBLABULUOKAEBMCUQNOZAABUQJ
      ZHZPZPUMNOZUNUPPZPBFVBVCBVBBQUNUPBABFRABUOUMFSTUAUBUBUQUMUCZURVBVAVCUQUMN
      UDVDAUNUTUPABFUEVDUSUOUCUTUPUFUQUMUGABUSUOKUHUIUIDUJUK $.
      $( [29-Jul-2003] $)
  $}

  ${
    $d x y z $.  $d x A $.  $d x z ch $.  $d x ta $.  $d y z ph $.
    $( Substitutions. $)
    tfinds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds.5 $e |- ps $.
    $( Induction hypothesis for successors. $)
    tfinds.6 $e |- ( y e. On -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds.7 $e |- ( Lim x -> ( A. y e. x ch -> ph ) ) $.
    $( Principle of Transfinite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the
       substitutions we need.  The last three are the basis, the induction
       hypothesis for successors, and the induction hypothesis for limit
       ordinals.  Theorem Schema 4 of [Suppes] p. 197. $)
    tfinds $p |- ( A e. On -> ta ) $=
      ( vz cv con0 wcel wlim wral wi wn c0 wceq csuc wrex wo word eloni wa
      cuni wne w3a df-lim biimpr 3com23 3expa ex necon1bd com23 orduninsuc
      biimprd syl5d imp con1d orrd syl mpbiri a1d hbra1 ax-17 hbim wsbc raleq1
      sbequ sbie syl5bbr cbvralv hbs1 sbequ12 cbvral 3bitr4g biimpd visset
      sucid rcla4v ax-mp syl5 sylan9r wb adantl sylibrd r19.23ai jaoi syl6
      pm2.61d2 tfis3 ) ACEFGHJLFQZRSZWSTZCGWSUAZAUBZWTXAUCZWSUDUEZWSGQZUFZUEZGR
      UGZUHZXCWTWSUIZXDXJUBWSUJXKXDXJXKXDUKZXEXIXLXIXEXKXDXIUCZXEUBXKXDWSWSULUE
      ZXEXMXKXNXDXEXKXNXDXEUBXKXNUKZXAWSUDXOWSUDUMZXAXKXNXPXAXKXPXNXAXAXKXPXNUN
      WSUOUPUQURUSUTUSVAXKXNXMGWSVBVCVDVEVFVGUSVHXEXCXIXEAXBXEABMIVIVJXHXCGRXBA
      GCGWSVKAGVLVMXFRSZXHXCXQXHUKXBDAXHXBAFXGUAZXQDXHXBXRXHAFPQZVNZPWSUAXTPXGU
      AXBXRXTPWSXGVOCXTGPWSXFXSUEAFXFVNXTCAGPFVPACFGCFVLJVQVRVSAXTFPXGAPVLAFPVT
      AFPWAWBWCWDXQCDXRNXFXGSXRCUBXFGWEWFACFXFXGJWGWHWIWJXHADWKXQKWLWMUSWNWOWPO
      WQWR $.
      $( [6-Mar-2004] $) $( [16-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x ch $.  $d x th $.  $d x ta $.  $d y ph $.
    $( Substitutions. $)
    tfindsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    tfindsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg.6 $e |- ( ( ( y e. On /\ B e. On ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg.7 $e |- ( ( ( Lim x /\ B e. On ) /\ B (_ x ) ->
                   ( A. y e. x ( B (_ y -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` B ` instead of zero.  Remark in
       [TakeutiZaring] p. 57. $)
    tfindsg $p |- ( ( ( A e. On /\ B e. On ) /\ B (_ A ) -> ta ) $=
      ( con0 wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl eqeq2 syl6bir imp
      imbi12d wn imbi1d ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61ian imbi2d
      a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi eqcoms
      imim2i com4r onsssuc onelpsst suceloni sylan2 bitrd ancoms ex ax-1 syl8
      a2d com23 sylbird annim syl5ibr pm2.61d wlim wral pm2.27 r19.20sdv
      ad2antlr syld exp31 com3l com4t tfinds imp31 ) HQRIQRZIHSZEXAIFTZSZAUAZUA
      XAIUBSZBUAZUAXAIGTZSZCUAZUAZXAIXHUCZSZDUAZUAXAXBEUAZUAFGHXCUBUDZXEXGXAIUB
      UDZXPXEXGUEXQXPUFXDXFABXPXDXFUEXQXCUBIUGZUHXQXPABUEZXQXPXCIUDZXSIUBXCUIJU
      JUKULXPXEXFAUAXQUMZXGXPXDXFAXRUNYAXFABYAXFXSXFXQIUOUPUQURUSUTVAXCXHUDZXEX
      JXAYBXDXIACXCXHIUGKULVAXCXLUDZXEXNXAYCXDXMADXCXLIUGLULVAXCHUDZXEXOXAYDXDX
      BAEXCHIUGMULVAXABXFNVBXHQRZXAXJXNYEXAXJXNUAZYEXAUFZXMIXLUDZUAZYFXAYIYFUAY
      EYIXJXMXADYIXMXADUAZUAXJYHYJXMYJXLIXLIUDYCXTUFZFVCYJFXLIXHGVDVEVFYKYJFXTX
      AAYCDXTABXAJNVGYCADLVHVIVJVKVLVMVBVNUHYGXMYHUMUFZYFYIUMYGYLXIYFXAYEXIYLUE
      XAYEUFXIIXLRZYLIXHVOXAXLQRYMYLUEYEIXLVPXHVQVRVSVTYGXJXIXNYGXICXNYGXICDXNY
      GXICDUAOWADXMWBWCWDWEWFXMYHWGWHWIWAWDXAXDXCWJZXKGXCWKZAYNXAXDYOAUAZYNXAXD
      YPYNXAUFXDUFYOXJGXCWKZAXAYOYQUAYNXDXAXKXJGXCXAXJWLWMWNPWOWPWQWRWSWT $.
      $( [6-Mar-2004] $) $( [5-Mar-2004] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    tfindsg2.1 $e |- ( x = suc B -> ( ph <-> ps ) ) $.
    tfindsg2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg2.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg2.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg2.6 $e |- ( ( y e. On /\ B e. y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg2.7 $e |- ( ( Lim x /\ B e. x ) ->
                   ( A. y e. x ( B e. y -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` suc B ` instead of zero. $)
    tfindsg2 $p |- ( ( A e. On /\ B e. A ) -> ta ) $=
      ( con0 wcel wa csuc wss onelon sucelon sylib word wi eloni ordsucss syl
      imp sylbir cv wb ordelsuc sylan2 ancoms ex adantr sylbird sylan2br wlim
      wral anassrs imbi1d ralbidva imbi12d cvv visset limelon mpan mpbid
      tfindsg exp31 imp3a mp2and ) HQRZIHRZSZITZQRZVSHUAZEVRIQRZVTHIUBIUCZUDVPV
      QWAVPHUEVQWAUFHUGIHUHUIUJVPVTWASEUFVQVPVTWAEVPVTWAEABCDEFGHVSJKLMVTWBBWCN
      UKGULZQRZVTSVSWDUAZCDUFZWEWBWFWGUFVTWEWBSWFIWDRZWGWBWEWHWFUMZWBWDUEZWIWEI
      WDQUNZWDUGZUOUPWEWHWGUFWBWEWHWGOUQURUSWCUTUJFULZVAZVTSVSWMUAZWFCUFZGWMVBZ
      AUFZWNWBWOWRUFZVTWNWBSIWMRZWHCUFZGWMVBZAUFZUFZWSWNXDWBWNWTXCPUQURWBWNXDWS
      UMZWBWMQRZXEWNWBXFSZWTWOXCWRWBWMUEWTWOUMXFIWMQUNWMUGUOXGXBWQAXGXAWPGWMXGW
      DWMRZSWHWFCWBXFXHWIWBWJWIXFXHSZWKXIWEWJWMWDUBWLUIUOVCVDVEVDVFWMVGRWNXFFVH
      WMVGVIVJUOUPVKWCUTUJVLVMVNURVO $.
      $( [12-Jan-2005] $) $( [5-Jan-2005] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    tfindes.1 $e |- [ (/) / x ] ph $.
    tfindes.2 $e |- ( x e. On -> ( ph -> [ suc x / x ] ph ) ) $.
    tfindes.3 $e |- ( Lim y -> ( A. x e. y ph -> [ y / x ] ph ) ) $.
    $( Transfinite Induction with explicit substitution.  The first hypothesis
       is the basis, the second is the induction hypothesis for successors, and
       the third is the induction hypothesis for limit ordinals.    Theorem
       Schema 4 of [Suppes] p. 197. $)
    tfindes $p |- ( x e. On -> ph ) $=
      ( vz cv wsbc c0 csuc dfsbcq sbequ sbequ12r con0 wcel wi ax-17 hbs1
      visset sucex hbsbc1v hbim wceq eleq1 sbequ12 wb suceq syl imbi12d chvar
      wlim wral cbvral syl5ibr tfinds ) ABCHZIZABJIABGHZIZABUSKZIZACGBHZABUQJLA
      CGBMABUQVALACBNDVCOPZAABVCKZIZQZQUSOPZUTVBQZQBGVHVIBVHBRUTVBBABGSZABVAUSG
      TUAUBUCUCVCUSUDZVDVHVGVIVCUSOUEVKAUTVFVBABGUFZVKVEVAUDVFVBUGVCUSUHABVEVAL
      UIUJUJEUKUQULABUQUMURUTGUQUMFAUTBGUQAGRVJVLUNUOUP $.
      $( [6-Mar-2004] $) $( [5-Mar-2004] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    tfinds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    tfinds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis for successors. $)
    tfinds2.5 $e |- ( y e. On -> ( ta -> ( ch -> th ) ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds2.6 $e |- ( Lim x -> ( ta -> ( A. y e. x ch -> ph ) ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first three hypotheses establish the substitutions we need.  The
       last three are the basis and the induction hypotheses (for successor and
       limit ordinals respectively).  Theorem Schema 4 of [Suppes] p. 197.  The
       wff ` ta ` is an auxiliary antecedent to help shorten proofs using this
       theorem. $)
    tfinds2 $p |- ( x e. On -> ( ta -> ph ) ) $=
      ( wi c0 wsbc 0ex cv wceq imbi2d sbcie mpbir con0 wcel csuc a2d sbimi cvv
      wb visset sbcel1gv ax-mp sbim 3imtr3 bicomd equcoms sucex sbbii suceq
      sbcco2 bitr3 3imtr3g wlim wral r19.21v syl5ib ax-17 limeq sbie sbralie
      syl5ibr tfindes ) EANZFGVMFOPEBNZKVMVNFOQFRZOSABEHTUAUBVOUCUDZECNZGVOPZED
      NZGVOPZVMVMFVOUEZPZGRZUCUDZGVOPZVQVSNZGVOPVPVRVTNWDWFGFWDECDLUFUGVOUHUDWE
      VPUIFUJZGVOUCUHUKULVQVSGFUMUNVQVMGVOWGWCVOSCAECAUIFGVOWCSACIUOUPTZUAVTVMF
      WCUEZPZGVOPWBWJVSGFVMVSFWIWCGUJUQVOWISADEJTUAURVMFGWAWIVOWCUSUTVAVBWCVCZV
      QGVOVDZFWCPZVMFWCPZVMFWCVDVOVCZFWCPWLVMNZFWCPWKWMWNNWOWPFGWOECGVOVDZNVMWL
      WOEWQAMUFECGVOVEVFUGWOWKFGWKFVGVOWCVHVIWLVMFGUMUNVQVMGFWHVJVKVL $.
      $( [6-Sep-2004] $) $( [4-Sep-2004] $)
  $}

  ${
    $v et $. $( Greek eta $)
    tfinds3.et $f wff et $.
    $d y ph $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d x y et $.
    $( Substitutions. $)
    tfinds3.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds3.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds3.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds3.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds3.5 $e |- ( et -> ps ) $.
    $( Induction hypothesis for successors. $)
    tfinds3.6 $e |- ( y e. On -> ( et -> ( ch -> th ) ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds3.7 $e |- ( Lim x -> ( et -> ( A. y e. x ch -> ph ) ) ) $.
    $( Principle of Transfinite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the
       substitutions we need.  The last three are the basis, the induction
       hypothesis for successors, and the induction hypothesis for limit
       ordinals. $)
    tfinds3 $p |- ( A e. On -> ( et -> ta ) ) $=
      ( wi cv c0 wceq imbi2d csuc con0 wcel a2d wlim wral r19.21v syl5ib
      tfinds ) IAQZIBQICQZIDQIEQFGHFRZSTABIJUAUMGRZTACIKUAUMUNUBTADILUAUMHTAEIM
      UANUNUCUDICDOUEUMUFZICGUMUGZQUKULGUMUGUOIUPAPUEICGUMUHUIUJ $.
      $( [7-Jan-2005] $) $( [6-Jan-2005] $)
  $}

  ${
    $d x A $.
    $( An ordinal subclass of non-limit ordinals is a class of natural numbers.
       Exercise 7 of [TakeutiZaring] p. 42. $)
    ssnlim $p |- ( ( Ord A /\ A (_ { x e. On | -. Lim x } ) -> A (_ om ) $=
      ( word cv wlim wn con0 crab wss wa com wcel limom ssel wceq limeq negbid
      elrab pm3.27bd syl6 mt2i adantl wb ordom ordtri1 mpan2 adantr mpbird ) BC
      ZBADZEZFZAGHZIZJBKIZKBLZFZUNUQUIUNUPKEZMUNUPKUMLZURFZBUMKNUSKGLUTULUTAKGU
      JKOUKURUJKPQRSTUAUBUIUOUQUCZUNUIKCVAUDBKUEUFUGUH $.
      $( [3-Nov-2004] $) $( [2-Nov-2004] $)
  $}

