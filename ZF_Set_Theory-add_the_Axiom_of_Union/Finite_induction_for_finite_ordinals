$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Finite induction (for finite ordinals)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    find.1 $e |- ( A C_ om /\ (/) e. A /\ A. x e. A suc x e. A ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of [TakeutiZaring] p. 43.  The simpler hypothesis shown here was
       suggested in an email from "Colin" on 1-Oct-2001.  The hypothesis states
       that ` A ` is a set of natural numbers, zero belongs to ` A ` , and
       given any member of ` A ` the member's successor also belongs to
       ` A ` .  The conclusion is that every natural number is in ` A ` .  (The
       proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    find $p |- A = om $=
      ( com wss c0 wcel cv csuc wral simp1i wi wa w3a 3simpc ax-mp df-ral alral
      wal sylbi anim2i peano5 eqssi ) BDBDEZFBGZAHZIBGZABJZCKUEUFBGUGLZADJZMZDB
      EUEUHMZUKUDUEUHNULCUDUEUHOPUHUJUEUHUIASUJUGABQUIADRTUAPABUBPUC $.
      $( [27-Aug-2011] $) $( [22-Feb-2004] $)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    finds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    finds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    finds.5 $e |- ps $.
    $( Induction hypothesis. $)
    finds.6 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema), using implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.  Theorem
       Schema 22 of [Suppes] p. 136. $)
    finds $p |- ( A e. om -> ta ) $=
      ( com wcel cab c0 cv elab csuc wi wral wss 0ex mpbir visset sucex 3imtr4g
      rgen peano5 mp2an sseli elabg mpbid ) HOPHAFQZPEOUPHRUPPZGSZUPPZURUAZUPPZ
      UBZGOUCOUPUDUQBMABFRUEITUFVBGOUROPCDUSVANACFURGUGZJTADFUTURVCUHKTUIUJGUPU
      KULUMAEFHOLUNUO $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    findsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsg.5 $e |- ( B e. om -> ps ) $.
    $( Induction hypothesis. $)
    findsg.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B C_ y ) -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema), using implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.  The
       basis of this version is an arbitrary natural number ` B ` instead of
       zero. $)
    findsg $p |- ( ( ( A e. om /\ B e. om ) /\ B C_ A ) -> ta ) $=
      ( wcel wss wi wceq wa com cv c0 wb sseq2 adantl eqeq2 syl6bir imp imbi12d
      wn imbi1d ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61ian imbi2d a1d visset
      wex sucex eqvinc syl5ibr biimpd sylan9r exlimiv sylbi eqcoms imim2i com4r
      csuc wne df-ne anbi2i annim bitri con0 nnon onsssuc suceloni sylan2 bitrd
      onelpss syl2an ancoms ax-1 syl8 com23 sylbird syl5bir pm2.61d finds imp31
      ex a2d ) HUAPIUAPZIHQZEWRIFUBZQZARZRWRIUCQZBRZRWRIGUBZQZCRZRWRIXEVMZQZDRZ
      RWRWSERZRFGHWTUCSZXBXDWRIUCSZXLXBXDUDXMXLTXAXCABXLXAXCUDXMWTUCIUEZUFXMXLA
      BUDZXMXLWTISZXOIUCWTUGJUHUIUJXLXBXCARXMUKZXDXLXAXCAXNULXQXCABXQXCXOXCXMIU
      MUNUOUPUQURUSWTXESZXBXGWRXRXAXFACWTXEIUEKUJUSWTXHSZXBXJWRXSXAXIADWTXHIUEL
      UJUSWTHSZXBXKWRXTXAWSAEWTHIUEMUJUSWRBXCNUTXEUAPZWRXGXJYAWRXGXJRZYAWRTZXII
      XHSZRZYBWRYEYBRYAYEXGXIWRDYEXIWRDRZRXGYDYFXIYFXHIXHISXSXPTZFVBYFFXHIXEGVA
      VCVDYGYFFXPWRAXSDWRAXPBNJVEXSADLVFVGVHVIVJVKUTVLUFYEUKZXIIXHVNZTZYCYBYJXI
      YDUKZTYHYIYKXIIXHVOVPXIYDVQVRYCYJXFYBWRYAXFYJUDZWRIVSPZXEVSPZYLYAIVTXEVTY
      MYNTXFIXHPZYJIXEWAYNYMXHVSPYOYJUDXEWBIXHWEWCWDWFWGYCXGXFXJYCXFCXJYCXFCDXJ
      YCXFCDROWPDXIWHWIWQWJWKWLWMWPWQWNWO $.
      $( [16-Sep-1995] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $.  $d y ph $.
    $( Substitutions. $)
    finds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis. $)
    finds2.5 $e |- ( y e. om -> ( ta -> ( ch -> th ) ) ) $.
    $( Principle of Finite Induction (inference schema), using implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136. $)
    finds2 $p |- ( x e. om -> ( ta -> ph ) ) $=
      ( cv com wcel wi c0 wceq imbi2d elab cab csuc wral wss mpbir visset sucex
      0ex a2d 3imtr4g rgen peano5 mp2an sseli abid sylib ) FMZNOUQEAPZFUAZOURNU
      SUQQUSOZGMZUSOZVAUBZUSOZPZGNUCNUSUDUTEBPZKURVFFQUHUQQRABEHSTUEVEGNVANOZEC
      PZEDPZVBVDVGECDLUIURVHFVAGUFZUQVARACEISTURVIFVCVAVJUGUQVCRADEJSTUJUKGUSUL
      UMUNURFUOUP $.
      $( [29-Nov-2002] $)
  $}

  ${
    $d x y $.  $d x ps $.  $d x ch $.  $d x th $.  $d y ph $.
    $( Substitutions. $)
    finds1.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds1.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds1.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds1.4 $e |- ps $.
    $( Induction hypothesis. $)
    finds1.5 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema), using implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136. $)
    finds1 $p |- ( x e. om -> ph ) $=
      ( cv com wcel c0 wceq eqid a1i wi a1d finds2 mpi ) ELMNOOPZAOQABCDUCEFGHI
      BUCJRFLMNCDSUCKTUAUB $.
      $( [22-Mar-2006] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    findes.1 $e |- [ (/) / x ] ph $.
    findes.2 $e |- ( x e. om -> ( ph -> [ suc x / x ] ph ) ) $.
    $( Finite induction with explicit substitution.  The first hypothesis is
       the basis and the second is the induction hypothesis.  Theorem Schema 22
       of [Suppes] p. 136.  See ~ tfindes for the transfinite version.
       (Contributed by Raph Levien, 9-Jul-2003.) $)
    findes $p |- ( x e. om -> ph ) $=
      ( vz vy cv wsbc c0 csuc dfsbcq sbequ sbequ12r com wcel wi ax-17 hbim wceq
      imbi12d hbs1 visset sucex hbsbc1v eleq1 sbequ12 wb suceq syl chvar finds
      ) ABEGZHABIHABFGZHZABUMJZHZAEFBGZABULIKAEFBLABULUOKAEBMCUQNOZAABUQJZHZPZP
      UMNOZUNUPPZPBFVBVCBVBBQUNUPBABFUAABUOUMFUBUCUDRRUQUMSZURVBVAVCUQUMNUEVDAU
      NUTUPABFUFVDUSUOSUTUPUGUQUMUHABUSUOKUITTDUJUK $.
      $( [9-Jul-2003] $)
  $}


