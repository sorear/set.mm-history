$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Founded and well-ordering relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new constant symbols. $)
  $c Fr $. $( Founded predicate symbol (read: 'founded'). $)
  $c We $. $( Well-ordering predicate symbol (read: 'well orders') $)

  $( Extend wff notation to include the founded predicate.  Read:  '` R ` is a
     founded relation on ` A ` .' $)
  wfr $a wff R Fr A $.

  $( Extend wff notation to include the well-ordering predicate.  Read:  '`R `
     well-orders ` A ` .' $)
  wwe $a wff R We A $.

  ${
    $d x y z R $.  $d x y z A $.
    $( Define a founded relation.  For alternate definitions, see ~ dffr2 and
       ~ dffr3 . $)
    df-fr $a |- ( R Fr A <-> A. x ( ( x (_ A /\ x =/= (/) ) ->
                E. y e. x A. z e. x -. z R y ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Property of founded relation (one direction of definition). $)
    fri $p |- ( ( ( B e. C /\ R Fr A ) /\ ( B (_ A /\ B =/= (/) ) ) ->
                E. x e. B A. y e. B -. y R x ) $=
      ( vz wcel wfr wss c0 wne wa cv wbr wn wral wrex wi wal wceq sseq1 neeq1
      anbi12d raleq1 rexeqd imbi12d cla4gv df-fr syl5ib imp31 ) DEHZCFIZDCJZDKL
      ZMZBNANFOPZBDQZADRZULGNZCJZUTKLZMZUQBUTQZAUTRZSZGTUPUSSZUMVFVGGDEUTDUAZVC
      UPVEUSVHVAUNVBUOUTDCUBUTDKUCUDVDURAUTDUQBUTDUEUFUGUHGABCFUIUJUK $.
      $( [3-Apr-2007] $) $( [18-Mar-1997] $)
  $}

  ${
    $d x y w v R $.  $d y z w v R $.  $d x w v A $.
    $( Alternate definition of founded relation.  Similar to Definition 6.21 of
       [TakeutiZaring] p. 30. $)
    dffr2 $p |- ( R Fr A <-> A. x ( ( x (_ A /\ x =/= (/) ) ->
                E. y e. x ( x i^i { z | z R y } ) = (/) ) ) $=
      ( vv vw wfr cv wss c0 wne wa wbr wn wral wrex wi wal cab cin wceq df-fr
      wcel disj visset breq1 elab negbii ralbii bitr rexbii breq2 negbid
      ralbidv cbvrexv imbi2i albii bitr4 ) DEHAIZDJUTKLMZFIZGIZENZOZFUTPZGUTQZR
      ZASVAUTCIZBIZENZCTZUAKUBZBUTQZRZASAGFDEUCVOVHAVNVGVAVNVBVJENZOZFUTPZBUTQV
      GVMVRBUTVMVBVLUDZOZFUTPVRFUTVLUEVTVQFUTVSVPVKVPCVBFUFVIVBVJEUGUHUIUJUKULV
      RVFBGUTVJVCUBZVQVEFUTWAVPVDVJVCVBEUMUNUOUPUKUQURUS $.
      $( [1-Feb-2007] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y z R $.  $d z A $.  $d x z B $.
    frc.1 $e |- B e. V $.
    $( Property of founded relation (one direction of definition using class
       variables). $)
    frc $p |- ( ( R Fr A /\ B (_ A /\ B =/= (/) ) ->
                E. x e. B ( B i^i { y | y R x } ) = (/) ) $=
      ( vz wfr wss c0 wne cv wbr cab cin wceq wrex wa wi wal dffr2 sseq1 neeq1
      anbi12d ineq1 eqeq1d rexeqd imbi12d cla4v sylbi 3impib ) CEHZDCIZDJKZDBLA
      LEMBNZOZJPZADQZULGLZCIZUSJKZRZUSUOOZJPZAUSQZSZGTUMUNRZURSZGABCEUAVFVHGDFU
      SDPZVBVGVEURVIUTUMVAUNUSDCUBUSDJUCUDVDUQAUSDVIVCUPJUSDUOUEUFUGUHUIUJUK $.
      $( [1-Feb-2007] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Subset theorem for the founded predicate.  Exercise 1 of
       [TakeutiZaring] p. 31. $)
    frss $p |- ( A (_ B -> ( R Fr B -> R Fr A ) ) $=
      ( vx vz vy wss cv c0 wne wa wbr cab cin wceq wrex wi wal wfr sstr2 com12
      anim1d imim1d 19.20dv dffr2 3imtr4g ) ABGZDHZBGZUHIJZKZUHEHFHCLEMNIOFUHPZ
      QZDRUHAGZUJKZULQZDRBCSACSUGUMUPDUGUOUKULUGUNUIUJUNUGUIUHABTUAUBUCUDDFEBCU
      EDFEACUEUF $.
      $( [3-Apr-1994] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.
    $( Equality theorem for the founded predicate. $)
    freq1 $p |- ( R = S -> ( R Fr A <-> S Fr A ) ) $=
      ( vx vz vy wceq cv wss c0 wne wa wbr wn wral wrex wi wal wfr breq negbid
      rexralbidv imbi2d albidv df-fr 3bitr4g ) BCGZDHZAIUHJKLZEHZFHZBMZNZEUHOFU
      HPZQZDRUIUJUKCMZNZEUHOFUHPZQZDRABSACSUGUOUSDUGUNURUIUGUMUQFEUHUHUGULUPUJU
      KBCTUAUBUCUDDFEABUEDFEACUEUF $.
      $( [9-Mar-1997] $)
  $}

  $( Equality theorem for the founded predicate. $)
  freq2 $p |- ( A = B -> ( R Fr A <-> R Fr B ) ) $=
    ( wceq wfr wss wa wi wb frss anim12i eqss bi 3imtr4 bicomd ) ABDZBCEZACEZAB
    FZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [3-Apr-1994] $)

  ${
    $d y z R $.  $d x y z $.  $d y z A $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30. $)
    frirr $p |- ( ( R Fr A /\ x e. A ) -> -. x R x ) $=
      ( vz vy wfr cv wcel wbr wn csn cab cin c0 wceq wrex wne visset snnz wss
      wi snex frc 3exp snss syl5ib mpii elsn breq2 abbidv ineq2d eqeq1d wa
      breq1 elab biimpr snid jctil elin sylibr n0i syl con2i syl6bi sylbi
      r19.23aiv syl6 imp ) BCFZAGZBHZVJVJCIZJZVIVKVJKZDGZEGZCIZDLZMZNOZEVNPZVMV
      IVKVNNQZWAVJARZSVIVNBTZWBWAUAVKVIWDWBWAEDBVNCVJUBUCUDVJBWCUEUFUGVTVMEVNVP
      VNHVPVJOZVTVMUAEVJUHWEVTVNVOVJCIZDLZMZNOZVMWEVSWHNWEVRWGVNWEVQWFDVPVJVOCU
      IUJUKULVLWIVLVJWHHZWIJVLVJVNHZVJWGHZUMWJVLWLWKWLVLWFVLDVJWCVOVJVJCUNUOUPV
      JWCUQURVJVNWGUSUTWHVJVAVBVCVDVEVFVGVH $.
      $( [2-Jan-1994] $)
  $}

  ${
    $d z w R $.  $d z w x $.  $d z w y $.  $d z w A $.
    $( A founded relation has no 2-cycle loops.  Special case of Proposition
       6.23 of [TakeutiZaring] p. 30. $)
    fr2nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x R y /\ y R x ) ) $=
      ( vz vw wfr cv wcel wbr wa wn cpr wss cab cin c0 wceq wrex wne visset
      prnz zfpair2 frc mp3an3 wo breq2 abbidv ineq2d neeq1d brab1 pri2 inelcm
      mpan sylbi syl5cbir pri1 jaao elpr syl5ib com12 necon2bd r19.23aiv syl
      prss sylan2b ancom2s ) CDGZBHZCIZAHZCIZVKVIDJZVIVKDJZKZLZVHVIVKMZCNZVPVJV
      LKVHVRKVQEHZFHZDJZEOZPZQRZFVQSZVPVHVRVQQTWEVIVKBUAZUBFECVQDBAUCUDUEWDVPFV
      QVTVQIZVOWCQVOWGWCQTZVOVTVIRZVTVKRZUFWHWGVMWIWHVNWJWIWHVQVSVIDJZEOZPZQTZV
      MWIWCWMQWIWBWLVQWIWAWKEVTVIVSDUGUHUIUJVMVKWLIZWNABEDUKVKVQIWOWNVIVKAUAZUL
      VKVQWLUMUNUOUPWJWHVQVSVKDJZEOZPZQTZVNWJWCWSQWJWBWRVQWJWAWQEVTVKVSDUGUHUIU
      JVNVIWRIZWTBAEDUKVIVQIXAWTVIVKWFUQVIVQWRUMUNUOUPURVTVIVKFUAUSUTVAVBVCVDVI
      VKCWFWPVEVFVG $.
      $( [30-May-1994] $)
  $}

  ${
    $d w v x $.  $d w v y $.  $d w v z $.  $d w v R $.  $d w v A $.
    $( A founded relation has no 3-cycle loops.  Special case of
       Proposition 6.23 of [TakeutiZaring] p. 30. $)
    fr3nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x R y /\ y R z /\ z R x ) ) $=
      ( vw vv wfr cv ctp wss wbr w3a wn wcel wa cab cin c0 wceq wrex wne
      visset tpnz tpex frc mp3an3 w3o wi 3jao breq2 abbidv ineq2d neeq1d brab1
      tpi3 inelcm mpan sylbi syl5cbir tpi1 tpi2 syl3an eltp syl5ib com12
      necon2bd r19.23aiv syl 3anrot tpss bitr sylan2b ) DEHZBIZCIZAIZJZDKZVQVOE
      LZVOVPELZVPVQELZMZNZVQDOZVODOZVPDOZMZVNVSPVRFIZGIZELZFQZRZSTZGVRUAZWDVNVS
      VRSUBWOVOVPVQBUCZUDGFDVREVOVPVQUEUFUGWNWDGVRWJVROZWCWMSWCWQWMSUBZWCWJVOTZ
      WJVPTZWJVQTZUHZWRWQWSWRUIWTWRUIXAWRUIXBWRUIVTWAWBWSWRWTXAUJWSWRVRWIVOELZF
      QZRZSUBZVTWSWMXESWSWLXDVRWSWKXCFWJVOWIEUKULUMUNVTVQXDOZXFABFEUOVQVROXGXFV
      OVPVQAUCZUPVQVRXDUQURUSUTWTWRVRWIVPELZFQZRZSUBZWAWTWMXKSWTWLXJVRWTWKXIFWJ
      VPWIEUKULUMUNWAVOXJOZXLBCFEUOVOVROXMXLVOVPVQWPVAVOVRXJUQURUSUTXAWRVRWIVQE
      LZFQZRZSUBZWBXAWMXPSXAWLXOVRXAWKXNFWJVQWIEUKULUMUNWBVPXOOZXQCAFEUOVPVROXR
      XQVOVPVQCUCZVBVPVRXOUQURUSUTVCWJVOVPVQGUCVDVEVFVGVHVIWHWFWGWEMVSWEWFWGVJV
      OVPVQDWPXSXHVKVLVM $.
      $( [10-Apr-1994] $)
  $}

  ${
    $d x y z R $.
    $( Any relation is founded on the empty set. $)
    fr0 $p |- R Fr (/) $=
      ( vx vz vy c0 wfr cv wss wne wa wbr cab cin wceq wrex wi dffr2 wn ss0
      nne sylibr imnan mpbi pm2.21i mpgbir ) EAFBGZEHZUFEIZJZUFCGDGAKCLMENDUFOZ
      PBBDCEAQUIUJUGUHRZPUIRUGUFENUKUFSUFETUAUGUHUBUCUDUE $.
      $( [17-Sep-1993] $)
  $}

  ${
    $d x A $.
    $( Irreflexivitiy of the epsilon relation:  a class founded by epsilon is
       not a member of itself. $)
    efrirr $p |- ( E Fr A  -> -. A e. A ) $=
      ( vx cep wfr wcel wn cv wi wceq freq2 eleq1 eleq2 bitrd negbid imbi12d
      wbr frirr ex epel negbii syl6ib pm2.01d vtoclg com12 ) ACDZAAEZUFUEUFFZBG
      ZCDZUHUHEZFZHUEUGHBAAUHAIZUIUEUKUGUHACJULUJUFULUJAUHEUFUHAUHKUHAALMNOUIUJ
      UIUJUHUHCPZFZUKUIUJUNBUHCQRUMUJBBSTUAUBUCUDUB $.
      $( [18-Apr-1994] $)
  $}

  $( A set founded by epsilon contains no 2-cycle loops. $)
  efrn2lp $p |- ( ( E Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x e. y /\ y e. x ) ) $=
    ( cep wfr cv wcel wa wbr wn fr2nr epel anbi12i negbii sylib ) CDEAFZCGBFZCG
    HHPQDIZQPDIZHZJPQGZQPGZHZJABCDKTUCRUASUBABLBALMNO $.
    $( [19-Apr-1994] $)

  $( A set founded by epsilon contains no 3-cycle loops. $)
  epne3 $p |- ( ( E Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x e. y /\ y e. z /\ z e. x ) ) $=
    ( cep wfr cv wcel w3a wa wbr wn fr3nr epel 3anbi123i negbii sylib ) DEFAGZD
    HBGZDHCGZDHIJRSEKZSTEKZTREKZIZLRSHZSTHZTRHZIZLABCDEMUDUHUAUEUBUFUCUGABNBCNC
    ANOPQ $.
    $( [19-Apr-1994] $)

  $( Similar to Theorem 7.2 of [TakeutiZaring] p. 35, of  except that the
     Axiom of Regularity is not required due to antecedent ` E Fr A ` . $)
  tz7.2 $p |- ( ( Tr A /\ E Fr A /\ B e. A ) -> ( B (_ A /\ B =/= A ) ) $=
    ( wtr cep wfr wcel wss wne wa trss wceq wn eleq1 negbid efrirr syl5cbir
    necon2ad anim12ii 3impia ) ACZADEZBAFZBAGZBAHZITUBUCUAUDABJUAUBBABAKZUBLAAF
    ZLUAUEUBUFBAAMNAOPQRS $.
    $( [28-Apr-2007] $) $( [4-May-1994] $)

  ${
    $d x y z $.  $d x z A $.
    $( An alternate way of saying that the epsilon relation is founded. $)
    dfepfr $p |- ( E Fr A <-> A. x ( ( x (_ A /\ x =/= (/) ) ->
                  E. y e. x ( x i^i y ) = (/) ) ) $=
      ( vz cep wfr cv wss c0 wne wa wbr cab cin wceq wrex wi wal dffr2 wcel
      epel abbii abid2 eqtr ineq2i eqeq1i rexbii imbi2i albii bitr ) CEFAGZCHUK
      IJKZUKDGZBGZELZDMZNZIOZBUKPZQZARULUKUNNZIOZBUKPZQZARABDCESUTVDAUSVCULURVB
      BUKUQVAIUPUNUKUPUMUNTZDMUNUOVEDDBUAUBDUNUCUDUEUFUGUHUIUJ $.
      $( [20-Apr-2007] $) $( [17-Feb-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    epfrc.1 $e |- B e. V $.
    $( A subset of an epsilon-founded class has a minimal element. $)
    epfrc $p |- ( ( E Fr A /\ B (_ A /\ B =/= (/) ) ->
                  E. x e. B ( B i^i x ) = (/) ) $=
      ( vy cep wfr wss c0 wne w3a cv wbr cab cin wceq wrex frc wcel epel abbii
      abid2 eqtr2 ineq2i eqeq1i rexbii sylibr ) BFGCBHCIJKCELZALZFMZENZOZIPZACQ
      CUIOZIPZACQAEBCFDRUOUMACUNULIUIUKCUKUHUISZENUIUJUPEEATUAEUIUBUCUDUEUFUG
      $.
      $( [21-Apr-2007] $) $( [17-Feb-2004] $)
  $}

  $( Define a well-ordering.  For an alternate definition see ~ dfwe2 . $)
  df-we $a |- ( R We A <-> ( R Fr A /\ R Or A ) ) $.

  ${
    $d x y z R $.  $d x y z A $.
    $( Alternate definition of well-ordering.  Definition 6.24(2) of
       [TakeutiZaring] p. 30. $)
    dfwe2 $p |- ( R We A <-> ( R Fr A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $=
      ( vz wwe wfr wor wa cv wbr wceq w3o wral df-we wcel wi wal solin ex
      19.21aivv r2al sylibr anim2i wpo wn w3a wo fr2nr 3adantr3 breq2 biimprd
      anim2d impcom nsyl imnan fr3nr df-3an negbii sylib jcad ioran syl6ibr
      imim1d 3orass orcom df-or 3bitr syl5ib frirr 3ad2antr1 jctild a2d
      19.20dv r3al 3imtr4g ralidm eqeq2 breq1 3orbi123d cbvralv ralbii bitr3
      df-po ancrd df-so imdistani impbi bitr ) CDFCDGZCDHZIZWJAJZBJZDKZWMWNLZWN
      WMDKZMZBCNZACNZIZCDOWLXAWKWTWJWKWMCPZWNCPZIZWRQZBRARWTWKXEABWKXDWRCWMWNDS
      TUAWRABCCUBUCUDWJWTWKWJWTCDUEZWTIWKWJWTXFWJWMEJZDKZWMXGLZXGWMDKZMZECNZBCN
      ZACNZWMWMDKUFZWOWNXGDKZIZXHQZIZECNBCNACNZWTXFWJXBXCXGCPZUGZXKQZERZBRZARYB
      XSQZERZBRZARXNXTWJYEYHAWJYDYGBWJYCYFEWJYBXKXSWJYBXKXSQWJYBIZXKXRXOYIXIXJU
      HZUFZXHQZXRXKYIXQYKXHYIXQXIUFZXJUFZIYKYIXQYMYNYIXQXIIZUFXQYMQYIWOWQIZYOWJ
      XBXCYPUFYAABCDUIUJXIXQYPXIXPWQWOXIWQXPWMXGWNDUKULUMUNUOXQXIUPUCYIXQXJIZUF
      ZXQYNQYIWOXPXJUGZUFYRABECDUQYSYQWOXPXJURUSUTXQXJUPUCVAXIXJVBVCVDXKXHYJUHY
      JXHUHYLXHXIXJVEXHYJVFYJXHVGVHVIWJXCXBXOYAACDVJVKVLTVMVNVNVNXKABECCCVOXSAB
      ECCCVOVPWSXMACWSWSBCNXMWRBCVQWSXLBCWRXKBECWNXGLWOXHWPXIWQXJWNXGWMDUKWNXGW
      MVRWNXGWMDVSVTWAWBWCWBABECDWDVPWEABCDWFVCWGWHWI $.
      $( [16-Mar-1997] $)
  $}

  $( Subset theorem for the well-ordering predicate.  Exercise 4 of
     [TakeutiZaring] p. 31. $)
  wess $p |- ( A (_ B -> ( R We B -> R We A ) ) $=
    ( wss wfr wor wa wwe frss soss anim12d df-we 3imtr4g ) ABDZBCEZBCFZGACEZACF
    ZGBCHACHNOQPRABCIABCJKBCLACLM $.
    $( [19-Apr-1994] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq1 $p |- ( R = S -> ( R We A <-> S We A ) ) $=
    ( wceq wfr wor wa wwe freq1 soeq1 anbi12d df-we 3bitr4g ) BCDZABEZABFZGACEZ
    ACFZGABHACHNOQPRABCIABCJKABLACLM $.
    $( [9-Mar-1997] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq2 $p |- ( A = B -> ( R We A <-> R We B ) ) $=
    ( wceq wfr wor wa wwe freq2 soeq2 anbi12d df-we 3bitr4g ) ABDZACEZACFZGBCEZ
    BCFZGACHBCHNOQPRABCIABCJKACLBCLM $.
    $( [3-Apr-1994] $)

  $( A well-ordering is founded. $)
  wefr $p |- ( R We A -> R Fr A ) $=
    ( wwe wfr wor df-we pm3.26bi ) ABCABDABEABFG $.
    $( [22-Apr-1994] $)

  $( A well-ordering is a strict ordering. $)
  weso $p |- ( R We A -> R Or A ) $=
    ( wwe wfr wor df-we pm3.27bi ) ABCABDABEABFG $.
    $( [16-Mar-1997] $)

  $( The elements of an epsilon well-ordering are comparable. $)
  wecmpep $p |- ( ( E We A /\ ( x e. A /\ y e. A ) ) ->
                 ( x e. y \/ x = y \/ y e. x ) ) $=
    ( cep wor cv wcel wa wceq w3o wwe wbr solin epel pm4.2 3orbi123i sylib
    weso sylan ) CDEZAFZCGBFZCGHZUAUBGZUAUBIZUBUAGZJZCDKTUCHUAUBDLZUEUBUADLZJUG
    CUAUBDMUHUDUEUEUIUFABNUEOBANPQCDRS $.
    $( [17-May-1994] $)

  $( An epsilon well-ordering is a transitive relation. $)
  wetrep $p |- ( ( E We A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
             ( ( x e. y /\ y e. z ) -> x e. z ) ) $=
    ( cep wwe cv wcel w3a wa wbr wor wi sotr weso sylan epel anbi12i 3imtr3g )
    DEFZAGZDHBGZDHCGZDHIZJUAUBEKZUBUCEKZJZUAUCEKZUAUBHZUBUCHZJUAUCHDELUDUGUHMTD
    UAUBUCENDEOPUEUIUFUJABQBCQRACQS $.
    $( [22-Apr-1994] $)

  ${
    $d x y z R $.  $d y z A $.  $d x y z B $.
    $( A non-empty (possibly proper) subclass of a class well-ordered by ` E `
       has a minimal element.  Special case of Proposition 6.26 of
       [TakeutiZaring] p. 31. $)
    wefrc $p |- ( ( E We A /\ B (_ A /\ B =/= (/) ) ->
               E. x e. B ( B i^i x ) = (/) ) $=
      ( vy vz cep wwe wss c0 wne cv cin wceq wrex wi wess wcel wex wa ineq2
      eqeq1d rcla4ev ex adantl inss1 wfr visset inex2 epfrc wefr syl3an1 3exp
      mpi elin anbi1i anass bitr rexbii2 syl6ib adantr wral w3a wetrep exp3a
      df-3an 3anrot bitr3 sylan2b exp44 imp com34 imp3a syl5ib imp4a com23
      r19.21adv dfss3 syl6ibr dfss in23 eqeq2i biimp biimprd syl6 r19.22dv
      syld pm2.61dne 19.23adv ne0 syl6com 3imp ) BFGZCBHZCIJZCAKZLZIMZACNZWMWLC
      FGZWNWROCBFPWSDKZCQZDRWRWNWSXAWRDWSXAWRWSXASZWRCWTLZIXAXCIMZWROWSXAXDWRWQ
      XDAWTCWOWTMWPXCIWOWTCTUAUBUCUDXBXCIJZWOWTQZXCWOLZIMZSZACNZWRWSXEXJOXAWSXE
      XHAXCNZXJWSXCCHZXEXKOCWTUEWSXLXEXKCFUFXLXEXKWSACXCWTCDUGUHUICFUJUKULUMXHX
      IAXCCWOXCQZXHSWOCQZXFSZXHSXNXISXMXOXHWOCWTUNUOXNXFXHUPUQURUSUTXBXIWQACXBX
      NXFXHWQXBXNXFXHWQOZXBXOWPWTHZXPXBXOEKZWTQZEWPVAXQXBXOXSEWPXBXRWPQZXOXSXBX
      TXNXFXSXBXRCQZXRWOQZSXNXFXSOZOZXTXBYAYBYDXBYAXNYBYCWSXAYAXNYBYCOZOOWSXAYA
      XNYEWSYAXNXAVBZYEXAYASXNSZWSYFSYBXFXSEADCVCVDYGXAYAXNVBYFXAYAXNVEXAYAXNVF
      VGVHVIVJVKVLXRCWOUNVMVNVOVPEWPWTVQVRXQWQXHXQWPXGIXQWPXGMZXQWPWPWTLZMYHWPW
      TVSYIXGWPCWOWTVTWAUQWBUAWCWDVDVNWEWFWGUCWHDCWIVMWJWK $.
      $( [18-Mar-2007] $) $( [17-Feb-2004] $)
  $}

  $( Any relation is a well-ordering of the empty set. $)
  we0 $p |- R We (/) $=
    ( c0 wwe wfr wor df-we fr0 so0 mpbir2an ) BACBADBAEBAFAGAHI $.
    $( [16-Mar-1997] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    wereu.1 $e |- B e. V $.
    $( A subset of a well-ordered set has a unique minimal element. $)
    wereu $p |- ( ( R We A /\ B (_ A /\ B =/= (/) ) ->
                E! x e. B A. y e. B -. y R x ) $=
      ( vz wwe wss c0 wne w3a cv wbr wn wral wrex wa wceq wi wreu wfr cvv wcel
      fri mpanl1 3impb wefr syl3an1 wo w3o wor solin weso sylan df-3or or23
      df-or 3bitr sylib ioran syl5ibr wess impcom breq1 negbid rcla4v im2anan9
      ancomsd imp syl5 exp4b pm2.43d 3adant3 r19.21aivv jca breq2 ralbidv reu4
      sylibr ) CEHZDCIZDJKZLZBMZAMZENZOZBDPZADQZWIWEGMZENZOZBDPZRZWFWKSZTZGDPAD
      PZRWIADUAWDWJWRCEUBZWBWCWJWAWSWBWCWJDUCUDWSWBWCRWJFABCDUCEUEUFUGCEUHUIWDW
      QAGDDWAWBWFDUDZWKDUDZRZWQTWCWAWBRZXBWQXCXBXBWOWPXCXBRWFWKENZOZWKWFENZOZRZ
      WPXBWORDEHZXBXHWPTXCXIXBRZXDXFUJZOZWPXHXJXDWPXFUKZXLWPTZDEULXBXMXIDWFWKEU
      MDEUNUOXMXDWPUJXFUJXKWPUJXNXDWPXFUPXDWPXFUQXKWPURUSUTXDXFVAVBWBWAXIDCEVCV
      DUOXBWOXHXBWNWIXHWTWNXEXAWIXGWMXEBWFDWEWFSWLXDWEWFWKEVEVFVGWHXGBWKDWEWKSW
      GXFWEWKWFEVEVFVGVHVIVJVKVLVMVNVOVPWIWNAGDWPWHWMBDWPWGWLWFWKWEEVQVFVRVSVT
      $.
      $( [5-Apr-2007] $) $( [18-Mar-1997] $)

    $( The unique minimal element of a subset of a well-ordered set. $)
    wereucl $p |- ( ( R We A /\ B (_ A /\ B =/= (/) ) ->
                U. { x e. B | A. y e. B -. y R x } e. B ) $=
      ( wwe wss c0 wne w3a cv wbr wn wral wreu crab cuni wcel wereu reucl syl
      ) CEGDCHDIJKBLALEMNBDOZADPUCADQRDSABCDEFTUCADUAUB $.
      $( [23-Sep-2007] $) $( [23-Sep-2007] $)
  $}


