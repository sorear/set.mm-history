$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The mapping operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c ^m $.  $( The mapping operation. $)
  $c ^pm $.  $( The mapping operation. $)

  $( Extend the definition of a class to include the mapping operation.  (Read
     for ` A ^m B ` , "the set of all functions that map from ` B ` to
     ` A ` .) $)
  cmap $a class ^m $.

  $( Extend the definition of a class to include the partial mapping
     operation.  (Read for ` A ^m B ` , "the set of all partial functions that
     map from ` B ` to ` A ` .) $)
  cpm $a class ^pm $.

  ${
    $d x y z f $.
    $( Define the mapping operation or set exponentiation.  The set of all
       functions that map from ` B ` to ` A ` is written ` ( A ^m B ) ` (see
       ~ mapval ).  Many authors write ` A ` followed by ` B ` as a superscript
       for this operation and rely on context to avoid confusion other
       exponentiation operations (e.g.  Definition 10.42 of [TakeutiZaring]
       p. 95).  Other authors show ` B ` as a prefixed superscript, which is
       read " ` A ` pre ` B ` " (e.g. definition of [Enderton] p. 52).
       Definition 8.21 of [Eisenberg] p. 125 uses the notation Map( ` B ` ,
       ` A ` ) for our ` ( A ^m B ) ` .  The up-arrow is used by Donald Knuth
       for iterated exponentiation (_Science_ 194, 1235-1242, 1976).  We adopt
       the first case of his notation (simple exponentiation) and subscript it
       with _m_ to distinguish it from other kinds of exponentiation. $)
    df-map $a |- ^m = { <. <. x , y >. , z >. | z = { f | f : y --> x } } $.

    $( Define the partial mapping operation.  A partial function from ` B ` to
       ` A ` is a function from a subset of ` B ` to ` A ` .  The set of all
       partial functions from ` B ` to ` A ` is written ` ( A ^pm B ) ` (see
       ~ pmvalg ).  A notation for this operation apparently does not appear in
       the literature.  We use ` ^pm ` to distinguish it from the less general
       set exponentiation operation ` ^m ` ( ~ df-map ) .  See ~ mapsspm for
       its relationship to set exponentiation. $)
    df-pm $a |- ^pm = { <. <. x , y >. , z >. | z =
                    { f | ( Fun f /\ f C_ ( y X. x ) ) } } $.
  $}

  $( Note:  an alternate way to express partial functions is as follows,
     which would be added after df-fun above.  The symbol would be _pfun.gif.
     This definition would be needed only if we need proper class partial
     functions, which seems unlikely.  While nice-looking, for ordinary usage
     it would inconveniently require a new set of equality theorems, etc. $)
  $( Define a partial function, which is a function from a subset of the
     domain ` A ` to the codomain ` B ` .  The notation of a stroke through
     the arrow is used by the Z language: see, for example,
     ~ http://staff.washington.edu/jon/z/dcs.html . $)
  $(
  df-fp $a |- ( F : A -|-> B <-> ( Fun F /\ F C_ ( X X. Y ) ) $.
  $)

  ${
    $d f x A $.  $d f x B $.
    $( When ` A ` is a proper class, the class of all functions mapping ` A `
       to ` B ` is empty.  Exercise 4.41 of [Mendelson] p. 255. $)
    mapprc $p |- ( -. A e. _V -> { f | f : A --> B } = (/) ) $=
      ( cvv wcel cv wf cab c0 wne wex abn0 cdm fdm visset dmex syl6eqelr
      19.23aiv sylbi necon1bi ) ADEZABCFZGZCHZIUDIJUCCKUAUCCLUCUACUCAUBMDABUBNU
      BCOPQRST $.
      $( [8-Dec-2003] $)

    $( The class of all partial functions from one set to another is a set. $)
    pmex $p |- ( ( A e. C /\ B e. D ) ->
                   { f | ( Fun f /\ f C_ ( A X. B ) ) } e. _V ) $=
      ( wcel wa cv wfun cxp wss cab cvv ancom abbii xpexg abssexg syl syl5eqel
      ) ACFBDFGZEHZIZUAABJZKZGZELUDUBGZELZMUEUFEUBUDNOTUCMFUGMFABCDPUBEUCMQRS
      $.
      $( [15-Nov-2007] $)

    $( The class of all functions mapping one set to another is a set.  Remark
       after Definition 10.24 of [Kunen] p. 31.  (Contributed by Raph Levien,
       4-Dec-2003.) $)
    mapex $p |- ( ( A e. C /\ B e. D ) -> { f | f : A --> B } e. _V ) $=
      ( wcel wa cv wf cab cxp cpw wss cvv fssxp ss2abi df-pw sseqtr4i xpexg
      pwexg syl ssexg sylancr ) ACFBDFGZABEHZIZEJZABKZLZMUINFZUGNFUGUEUHMZEJUIU
      FUKEABUEOPEUHQRUDUHNFUJABCDSUHNTUAUGUINUBUC $.
      $( [4-Dec-2003] $)
  $}

  ${
    $d f x y z $.
    $( Set exponentiation has a universal domain. $)
    fnmap $p |- ^m Fn ( _V X. _V ) $=
      ( vx vy vz vf cvv cv wf cab cmap wcel visset mapex mp2an wceq copab2 wa
      df-map pm3.2i biantrur oprabbii eqtri fnoprab2 ) ABCEEBFZAFZDFGDHZIUCEJZU
      DEJZUEEJBKZAKZUCUDEEDLMICFUENZABCOUGUFPZUJPZABCOABCDQUJULABCUKUJUGUFUIUHR
      STUAUB $.
      $( [8-Dec-2003] $)
  $}

  ${
    $d x y z f A $.  $d x y z f B $.
    $( The value of set exponentiation. ` ( A ^m B ) ` is the set of all
       functions that map from ` B ` to ` A ` .  Definition 10.24 of [Kunen]
       p. 24. $)
    mapvalg $p |- ( ( A e. C /\ B e. D ) ->
                  ( A ^m B ) = { f | f : B --> A } ) $=
      ( vx vy vz wcel wa cv wf cab cvv cmap co wceq mapex ancoms wi elisset
      feq3 abbidv feq2 copab2 df-map visset pm3.2i biantrur oprabbii eqtri
      oprabval2g 3expia syl2an mpd ) ACIZBDIZJBAEKZLZEMZNIZABOPUTQZUQUPVABADCER
      SUPANIZBNIZVAVBTUQACUABDUAVCVDVAVBFGHABNNGKZFKZURLZEMZUTOVEAURLZEMNVFAQVG
      VIEVFAVEURUBUCVEBQVIUSEVEBAURUDUCOHKVHQZFGHUEVFNIZVENIZJZVJJZFGHUEFGHEUFV
      JVNFGHVMVJVKVLFUGGUGUHUIUJUKULUMUNUO $.
      $( [8-Dec-2003] $)

    $( The value of the partial mapping operation. ` ( A ^pm B ) ` is the set
       of all partial functions that map from ` B ` to ` A ` . $)
    pmvalg $p |- ( ( A e. C /\ B e. D ) ->
                  ( A ^pm B ) = { f | ( Fun f /\ f C_ ( B X. A ) ) } ) $=
      ( vx vy vz wcel wa cv wfun cxp wss cab cvv cpm co wceq pmex ancoms wi
      elisset xpeq2 sseq2d anbi2d abbidv xpeq1 copab2 df-pm visset pm3.2i
      biantrur oprabbii eqtri oprabval2g 3expia syl2an mpd ) ACIZBDIZJEKZLZVBBA
      MZNZJZEOZPIZABQRVGSZVAUTVHBADCETUAUTAPIZBPIZVHVIUBVAACUCBDUCVJVKVHVIFGHAB
      PPVCVBGKZFKZMZNZJZEOZVGQVCVBVLAMZNZJZEOPVMASZVPVTEWAVOVSVCWAVNVRVBVMAVLUD
      UEUFUGVLBSZVTVFEWBVSVEVCWBVRVDVBVLBAUHUEUFUGQHKVQSZFGHUIVMPIZVLPIZJZWCJZF
      GHUIFGHEUJWCWGFGHWFWCWDWEFUKGUKULUMUNUOUPUQURUS $.
      $( [15-Nov-2007] $)
  $}

  ${
    $d f A $.  $d f B $.
    mapval.1 $e |- A e. _V $.
    mapval.2 $e |- B e. _V $.
    $( The value of set exponentiation (inference version). ` ( A ^m B ) ` is
       the set of all functions that map from ` B ` to ` A ` .  Definition
       10.24 of [Kunen] p. 24. $)
    mapval $p |- ( A ^m B ) = { f | f : B --> A } $=
      ( cvv wcel cmap co cv wf cab wceq mapvalg mp2an ) AFGBFGABHIBACJKCLMDEABF
      FCNO $.
      $( [8-Dec-2003] $)
  $}

  ${
    $d g A $.  $d g B $.  $d g C $.
    $( Membership relation for set exponentiation. $)
    elmapg $p |- ( ( A e. R /\ B e. S ) ->
                  ( C e. ( A ^m B ) <-> C : B --> A ) ) $=
      ( vg wcel wa cmap co cv wf cab mapvalg eleq2d cvv wi wb fex expcom
      adantl feq1 elab3g syl bitrd ) ADGZBEGZHZCABIJZGCBAFKZLZFMZGZBACLZUHUIULC
      ABDEFNOUHUNCPGZQZUMUNRUGUPUFUNUGUOBAECSTUAUKUNFCPBAUJCUBUCUDUE $.
      $( [17-Oct-2006] $)
  $}

  ${
    $d g A $.  $d f g B $.  $d g F $.
    elmap.1 $e |- A e. _V $.
    elmap.2 $e |- B e. _V $.
    $( Membership relation for set exponentiation. $)
    elmap $p |- ( F e. ( A ^m B ) <-> F : B --> A ) $=
      ( cvv wcel cmap co wf wb elmapg mp2an ) AFGBFGCABHIGBACJKDEABCFFLM $.
      $( [8-Dec-2003] $)

    $( Alternate expression for the value of set exponentiation. $)
    mapval2 $p |- ( A ^m B ) = ( ~P ( B X. A ) i^i { f | f Fn B } ) $=
      ( vg cmap co cxp cpw cv wfn cab cin wf wss wa wcel ffn fssxp jca crn
      rnss rnxpss sstr sylancl anim2i df-f sylibr impbii ancom bitri elmap
      elin visset elpw fneq1 elab anbi12i 3bitr4i eqriv ) FABGHZBAIZJZCKZBLZCMZ
      NZBAFKZOZVIVCPZVIBLZQZVIVBRVIVHRZVJVLVKQZVMVJVOVJVLVKBAVISBAVITUAVOVLVIUB
      ZAPZQVJVKVQVLVKVPVCUBZPVRAPVQVIVCUCBAUDVPVRAUEUFUGBAVIUHUIUJVLVKUKULABVID
      EUMVNVIVDRZVIVGRZQVMVIVDVGUNVSVKVTVLVIVCFUOZUPVFVLCVIWABVEVIUQURUSULUTVA
      $.
      $( [3-Nov-2007] $)

    $( The predicate "is a partial function." $)
    elpm $p |- ( F e. ( A ^pm B ) <-> ( Fun F /\ F C_ ( B X. A ) ) ) $=
      ( vg cpm co wcel cv wfun cxp wss wa cab cvv wceq pmvalg mp2an eleq2i
      xpex ssex adantl funeq sseq1 anbi12d elab3 bitri ) CABGHZICFJZKZUJBALZMZN
      ZFOZICKZCULMZNZUIUOCAPIBPIUIUOQDEABPPFRSTUNURFCUQCPIUPCULBAEDUAUBUCUJCQUK
      UPUMUQUJCUDUJCULUEUFUGUH $.
      $( [15-Nov-2007] $)

    $( The predicate "is a partial function." $)
    elpm2 $p |- ( F e. ( A ^pm B ) <-> ( F : dom F --> A /\ dom F C_ B ) ) $=
      ( cpm co wcel wfun cxp wss wa cdm wf elpm funssxp bitri ) CABFGHCICBAJKLC
      MZACNRBKLABCDEOBACPQ $.
      $( [15-Nov-2007] $)

    $( A total function is a partial function. $)
    fpm $p |- ( F : A --> B -> F e. ( B ^pm A ) ) $=
      ( wf cdm wss wa cpm co wcel ffdm elpm2 sylibr ) ABCFCGZBCFPAHICBAJKLABCMB
      ACEDNO $.
      $( [15-Nov-2007] $)

    $( Set exponentiation is a subset of partial maps. $)
    mapsspm $p |- ( A ^m B ) C_ ( A ^pm B ) $=
      ( vg cmap co cpm cv wcel wf elmap fpm sylbi ssriv ) EABFGZABHGZEIZPJBARKR
      QJABRCDLBARDCMNO $.
      $( [15-Nov-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x y R $.
    fvopabf4.1 $e |- C e. _V $.
    fvopabf4.2 $e |- D e. _V $.
    fvopabf4.3 $e |- R e. _V $.
    fvopabf4.4 $e |- ( x = A -> B = C ) $.
    fvopabf4.5 $e |- F = { <. x , y >. | ( x : D --> R /\ y = B ) } $.
    $( Special case of ~ fvopab4 for operator theorems. $)
    fvopabf4 $p |- ( A : D --> R -> ( F ` A ) = C ) $=
      ( wf cmap co wcel cfv wceq elmap cv wa copab anbi1i opabbii eqtr4i
      fvopab4 sylbir ) FGCNCGFOPZQCHRESGFCKJTABCDEUIHLHFGAUAZNZBUADSZUBZABUCUJU
      IQZULUBZABUCMUOUMABUNUKULGFUJKJTUDUEUFIUGUH $.
      $( [27-Nov-2007] $)
  $}

  ${
    $d f A $.  $d f B $.
    $( Set exponentiation is a subset of the power set of the cross product of
       its arguments. $)
    mapsspw $p |- ( B e. R -> ( A ^m B ) C_ ~P ( B X. A ) ) $=
      ( vf cvv wcel cmap co cxp cpw wss wa cv wf cab mapvalg fssxp ss2abi
      df-pw sseqtr4i a1i eqsstrd wn c0 cdm wrel relxp wfn wceq fnmap fndm
      ax-mp releqi mpbir oprprc1 0ss adantr pm2.61ian ) AEFZBCFZABGHZBAIZJZKZUS
      UTLZVABADMZNZDOZVCABECDPVHVCKVEVHVFVBKZDOVCVGVIDBAVFQRDVBSTUAUBUSUCZVDUTV
      JVAUDVCABGGUEZUFEEIZUFEEUGVKVLGVLUHVKVLUIUJVLGUKULUMUNUOUDVCKVJVCUPUAUBUQ
      UR $.
      $( [8-Dec-2006] $)
  $}

  ${
    $d f A $.
    map0e.1 $e |- A e. _V $.
    $( Set exponentiation with an empty exponent (ordinal number 0) is ordinal
       number 1.  Exercise 4.42(a) of [Mendelson] p. 255. $)
    map0e $p |- ( A ^m (/) ) = 1o $=
      ( vf c0 cmap co csn c1o cv wf cab wceq wfn crn wss wa fn0 anbi1i df-f
      0ss rneq rn0 syl6eq sseq1d mpbiri pm4.71i 3bitr4i abbii 0ex mapval df-sn
      3eqtr4i df1o2 eqtr4i ) ADEFZDGZHDACIZJZCKUQDLZCKUOUPURUSCUQDMZUQNZAOZPUSV
      BPURUSUTUSVBUQQRDAUQSUSVBUSVBDAOATUSVADAUSVADNDUQDUAUBUCUDUEUFUGUHADCBUIU
      JCDUKULUMUN $.
      $( [10-Dec-2003] $)

    $( Set exponentiation with an empty base is the empty set, provided the
       exponent is non-empty.  Theorem 96 of [Suppes] p. 89. $)
    map0b $p |- ( A =/= (/) -> ( (/) ^m A ) = (/) ) $=
      ( vf c0 wne cmap co cv wf cab 0ex mapval wex wceq abn0 cdm fdm crn wss
      frn ss0 syl dm0rn0 sylibr eqtr3d 19.23aiv sylbi necon1i syl5eq ) ADEDAFGA
      DCHZIZCJZDDACKBLULDADULDEUKCMADNZUKCOUKUMCUKUJPZADADUJQUKUJRZDNZUNDNUKUOD
      SUPADUJTUOUAUBUJUCUDUEUFUGUHUI $.
      $( [19-Mar-2007] $) $( [10-Dec-2003] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    map0.1 $e |- A e. _V $.
    map0.2 $e |- B e. _V $.
    $( Set exponentiation is empty iff the base is empty and the exponent is
       not empty.  Theorem 97 of [Suppes] p. 89. $)
    map0 $p |- ( ( A ^m B ) = (/) <-> ( A = (/) /\ B =/= (/) ) ) $=
      ( vf vx cmap co c0 wceq wne wa cv wf cab mapval eqeq1i wcel wex csn wss
      cxp snssi visset fconst fss mpan snex xpex feq1 cla4ev 3syl 19.23aiv n0
      abn0 3imtr4i necon4i sylbi c1o map0e df1o2 eqtri 0ex snnz eqnetri opreq2
      neeq1d mpbiri necon2i jca opreq1 map0b sylan9eq impbii ) ABGHZIJZAIJZBIKZ
      LVPVQVRVPBAEMZNZEOZIJVQVOWAIABECDPQAIWAIFMZARZFSVTESZAIKWAIKWCWDFWCWBTZAU
      AZBABWEUBZNZWDWBAUCBWEWGNWFWHBWBFUDUEBWEAWGUFUGVTWHEWGBWEDWBUHUIBAVSWGUJU
      KULUMFAUNVTEUOUPUQURBIVOIBIJZVOIKAIGHZIKWJITZIWJUSWKACUTVAVBIVCVDVEWIVOWJ
      IBIAGVFVGVHVIVJVQVRVOIBGHIAIBGVKBDVLVMVN $.
      $( [17-May-2007] $) $( [10-Dec-2003] $)

    $( The value of set exponentiation with a singleton exponent.  Theorem 98
       of [Suppes] p. 89. $)
    mapsn $p |- ( A ^m { B } ) = { f | E. y e. A f = { <. B , y >. } } $=
      ( cv cop csn wceq wrex cmap co wcel wf snex elmap wa wex crn wbr weu wfn
      ffn snid fneu sylancl cab euabsn cima wrel frel relimasn syl cdm imadmrn
      fdm imaeq2d syl5reqr eqtr3d eqeq1d exbidv syl5bb mpbid visset eleq2
      mpbiri frn sseld syl5 wfo dffn4 sylib fof feq3 syl5ibcom fsn syl6ib jcad
      eximdv mpd df-rex sylibr wss wf1o f1osn f1oeq1 f1of snssi fss syl2an
      expcom r19.23aiv impbii bitri abbi2i ) DGZCAGZHIZJZABKZDBCIZLMZWQXCNXBBWQ
      OZXABXBWQECPQXDXAXDWRBNZWTRZASZXAXDWQTZWRIZJZASZXGXDCWRWQUAZAUBZXKXDWQXBU
      CZCXBNXMXBBWQUDZCFUEAXBCWQUFUGXMXLAUHZXIJZASXDXKXLAUIXDXQXJAXDXPXHXIXDWQX
      BUJZXPXHXDWQUKXRXPJXBBWQULACWQUMUNXDXHWQWQUOZUJXRWQUPXDXSXBWQXBBWQUQURUSU
      TVAVBVCVDXDXJXFAXDXJXEWTXJWRXHNZXDXEXJXTWRXINWRAVEZUEXHXIWRVFVGXDXHBWRXBB
      WQVHVIVJXDXJXBXIWQOZWTXDXBXHWQOZXJYBXDXBXHWQVKZYCXDXNYDXOXBWQVLVMXBXHWQVN
      UNXHXIXBWQVOVPCWRWQFYAVQVRVSVTWAWTABWBWCWTXDABWTXEXDWTYBXIBWDXDXEWTXBXIWQ
      WEZYBWTYEXBXIWSWECWRFYAWFXBXIWQWSWGVGXBXIWQWHUNWRBWIXBXIBWQWJWKWLWMWNWOWP
      $.
      $( [10-Dec-2003] $)
  $}

  ${
    $d f A $.  $d f B $.  $d f C $.
    mapss.1 $e |- B e. _V $.
    mapss.2 $e |- C e. _V $.
    $( Subset inheritance for set exponentiation.  Theorem 99 of [Suppes]
       p. 89. $)
    mapss $p |- ( A C_ B -> ( A ^m C ) C_ ( B ^m C ) ) $=
      ( vf wss cv wf cab cmap co fss expcom ss2abdv cvv wcel wceq ssex mapvalg
      sylancl mapval a1i 3sstr4d ) ABGZCAFHZIZFJZCBUFIZFJZACKLZBCKLZUEUGUIFUGUE
      UICABUFMNOUEAPQCPQUKUHRABDSEACPPFTUAULUJRUEBCFDEUBUCUD $.
      $( [10-Dec-2003] $)
  $}

