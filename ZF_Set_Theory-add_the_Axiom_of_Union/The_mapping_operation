$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The mapping operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c ^m $.  $( The mapping operation. $)
  $c ^pm $.  $( The mapping operation. $)

  $( Extend the definition of a class to include the mapping operation.  (Read
     for ` A ^m B ` , "the set of all functions that map from ` B ` to
     ` A ` .) $)
  cmap $a class ^m $.

  $( Extend the definition of a class to include the partial mapping
     operation.  (Read for ` A ^m B ` , "the set of all partial functions that
     map from ` B ` to ` A ` .) $)
  cpm $a class ^pm $.

  ${
    $d x y f $.
    $( Define the mapping operation or set exponentiation.  The set of all
       functions that map from ` B ` to ` A ` is written ` ( A ^m B ) ` (see
       ~ mapval ).  Many authors write ` A ` followed by ` B ` as a superscript
       for this operation and rely on context to avoid confusion other
       exponentiation operations (e.g.  Definition 10.42 of [TakeutiZaring]
       p. 95).  Other authors show ` B ` as a prefixed superscript, which is
       read " ` A ` pre ` B ` " (e.g. definition of [Enderton] p. 52).
       Definition 8.21 of [Eisenberg] p. 125 uses the notation Map( ` B ` ,
       ` A ` ) for our ` ( A ^m B ) ` .  The up-arrow is used by Donald Knuth
       for iterated exponentiation (_Science_ 194, 1235-1242, 1976).  We adopt
       the first case of his notation (simple exponentiation) and subscript it
       with _m_ to distinguish it from other kinds of exponentiation. $)
    df-map $a |- ^m = ( x e. _V , y e. _V |-> { f | f : y --> x } ) $.

    $( Define the partial mapping operation.  A partial function from ` B ` to
       ` A ` is a function from a subset of ` B ` to ` A ` .  The set of all
       partial functions from ` B ` to ` A ` is written ` ( A ^pm B ) ` (see
       ~ pmvalg ).  A notation for this operation apparently does not appear in
       the literature.  We use ` ^pm ` to distinguish it from the less general
       set exponentiation operation ` ^m ` ( ~ df-map ) .  See ~ mapsspm for
       its relationship to set exponentiation. $)
    df-pm $a |- ^pm = ( x e. _V , y e. _V |->
                    { f e. ~P ( y X. x ) | Fun f } ) $.
  $}

  $( Note:  an alternate way to express partial functions is as follows,
     which would be added after df-fun above.  The symbol would be _pfun.gif.
     This definition would be needed only if we need proper class partial
     functions, which seems unlikely.  While nice-looking, for ordinary usage
     it would inconveniently require a new set of equality theorems, etc. $)
  $( Define a partial function, which is a function from a subset of the
     domain ` A ` to the codomain ` B ` .  The notation of a stroke through
     the arrow is used by the Z language: see, for example,
     ~ http://staff.washington.edu/jon/z/dcs.html . $)
  $(
  df-fp $a |- ( F : A -|-> B <-> ( Fun F /\ F C_ ( X X. Y ) ) $.
  $)

  ${
    $d f x A $.  $d f x B $.
    $( When ` A ` is a proper class, the class of all functions mapping ` A `
       to ` B ` is empty.  Exercise 4.41 of [Mendelson] p. 255. $)
    mapprc $p |- ( -. A e. _V -> { f | f : A --> B } = (/) ) $=
      ( cvv wcel cv wf cab c0 wne wex abn0 cdm fdm dmex syl6eqelr exlimiv sylbi
      vex necon1bi ) ADEZABCFZGZCHZIUDIJUCCKUAUCCLUCUACUCAUBMDABUBNUBCSOPQRT $.
      $( [8-Dec-2003] $)

    $( The class of all partial functions from one set to another is a set. $)
    pmex $p |- ( ( A e. C /\ B e. D ) ->
                   { f | ( Fun f /\ f C_ ( A X. B ) ) } e. _V ) $=
      ( wcel wa cv wfun cxp wss cab cvv ancom abbii xpexg abssexg syl syl5eqel
      ) ACFBDFGZEHZIZUAABJZKZGZELUDUBGZELZMUEUFEUBUDNOTUCMFUGMFABCDPUBEUCMQRS
      $.
      $( [15-Nov-2007] $)

    $( The class of all functions mapping one set to another is a set.  Remark
       after Definition 10.24 of [Kunen] p. 31.  (Contributed by Raph Levien,
       4-Dec-2003.) $)
    mapex $p |- ( ( A e. C /\ B e. D ) -> { f | f : A --> B } e. _V ) $=
      ( wcel wa cv cab cxp cpw wss cvv fssxp ss2abi df-pw sseqtr4i xpexg pwexg
      wf syl ssexg sylancr ) ACFBDFGZABEHZTZEIZABJZKZLUIMFZUGMFUGUEUHLZEIUIUFUK
      EABUENOEUHPQUDUHMFUJABCDRUHMSUAUGUIMUBUC $.
      $( [4-Dec-2003] $)
  $}

  ${
    $d f x y $.
    $( Set exponentiation has a universal domain. $)
    fnmap $p |- ^m Fn ( _V X. _V ) $=
      ( vx vy vf cvv cv wf cab cmap df-map wcel vex mapex mp2an fnmpt2i ) ABDDB
      EZAEZCEFCGZHABCIODJPDJQDJBKAKOPDDCLMN $.
      $( [8-Sep-2013] $) $( [8-Dec-2003] $)

    $( Partial function exponentiation has a universal domain. $)
    fnpm $p |- ^pm Fn ( _V X. _V ) $=
      ( vx vy vf cvv cv wfun cxp cpw crab cpm df-pm vex xpex pwex rabex fnmpt2i
      ) ABDDCEFZCBEZAEZGZHZIJABCKQCUATRSBLALMNOP $.
      $( [14-Nov-2013] $)
  $}

  ${
    $d x y f A $.  $d y f B $.
    $( The value of set exponentiation. ` ( A ^m B ) ` is the set of all
       functions that map from ` B ` to ` A ` .  Definition 10.24 of [Kunen]
       p. 24. $)
    mapvalg $p |- ( ( A e. C /\ B e. D ) ->
                  ( A ^m B ) = { f | f : B --> A } ) $=
      ( vx vy wcel wa cv wf cab cvv cmap co wceq mapex ancoms elex abbidv feq3
      wi feq2 df-map ovmpt2g 3expia syl2an mpd ) ACHZBDHZIBAEJZKZELZMHZABNOUMPZ
      UJUIUNBADCEQRUIAMHZBMHZUNUOUBUJACSBDSUPUQUNUOFGABMMGJZFJZUKKZELUMNURAUKKZ
      ELMUSAPUTVAEUSAURUKUATURBPVAULEURBAUKUCTFGEUDUEUFUGUH $.
      $( [8-Sep-2013] $) $( [8-Dec-2003] $)

    $( The value of the partial mapping operation. ` ( A ^pm B ) ` is the set
       of all partial functions that map from ` B ` to ` A ` . $)
    pmvalg $p |- ( ( A e. C /\ B e. D ) ->
                  ( A ^pm B ) = { f e. ~P ( B X. A ) | Fun f } ) $=
      ( vx vy wcel wa cv cxp cpw crab cvv cpm wceq elex pweq rabeq 3syl wfun co
      wss ssrab2 xpexg ancoms pwexg syl ssexg sylancr xpeq2 xpeq1 df-pm ovmpt2g
      wi 3expia syl2an mpd ) ACHZBDHZIZEJUAZEBAKZLZMZNHZABOUBVEPZVAVEVDUCVDNHZV
      FVBEVDUDVAVCNHZVHUTUSVIBADCUEUFVCNUGUHVEVDNUIUJUSANHZBNHZVFVGUOUTACQBDQVJ
      VKVFVGFGABNNVBEGJZFJZKZLZMZVEOVBEVLAKZLZMZNVMAPVNVQPVOVRPVPVSPVMAVLUKVNVQ
      RVBEVOVRSTVLBPVQVCPVRVDPVSVEPVLBAULVQVCRVBEVRVDSTFGEUMUNUPUQUR $.
      $( [8-Sep-2013] $) $( [15-Nov-2007] $)
  $}

  ${
    $d f A $.  $d f B $.
    mapval.1 $e |- A e. _V $.
    mapval.2 $e |- B e. _V $.
    $( The value of set exponentiation (inference version). ` ( A ^m B ) ` is
       the set of all functions that map from ` B ` to ` A ` .  Definition
       10.24 of [Kunen] p. 24. $)
    mapval $p |- ( A ^m B ) = { f | f : B --> A } $=
      ( cvv wcel cmap co cv wf cab wceq mapvalg mp2an ) AFGBFGABHIBACJKCLMDEABF
      FCNO $.
      $( [8-Dec-2003] $)
  $}

  ${
    $d g A $.  $d g B $.  $d g C $.
    $( Membership relation for set exponentiation. $)
    elmapg $p |- ( ( A e. V /\ B e. W ) ->
                  ( C e. ( A ^m B ) <-> C : B --> A ) ) $=
      ( vg wcel wa cmap co cv wf cab mapvalg eleq2d cvv wi wb fex expcom adantl
      feq1 elab3g syl bitrd ) ADGZBEGZHZCABIJZGCBAFKZLZFMZGZBACLZUHUIULCABDEFNO
      UHUNCPGZQZUMUNRUGUPUFUNUGUOBAECSTUAUKUNFCPBAUJCUBUCUDUE $.
      $( [17-Oct-2006] $)

    $( The predicate "is a partial function." $)
    elpmg $p |- ( ( A e. V /\ B e. W ) ->
                  ( C e. ( A ^pm B ) <-> ( Fun C /\ C C_ ( B X. A ) ) ) ) $=
      ( vg wcel wa cpm co cxp cpw wfun wss cv crab wb a1i cvv wi funeq elex syl
      pmvalg eleq2d elrab syl6bb ancom xpexg ancoms ssexg expcom anbi2d 3bitrd
      elpwg pm5.21ndd ) ADGZBEGZHZCABIJZGZCBAKZLZGZCMZHZVEVDHZVECVBNZHUSVACFOZM
      ZFVCPZGVFUSUTVKCABDEFUDUEVJVEFCVCVICUAUFUGVFVGQUSVDVEUHRUSVDVHVEUSCSGZVDV
      HVDVLTUSCVCUBRUSVBSGZVHVLTURUQVMBAEDUIUJVHVMVLCVBSUKULUCVLVDVHQTUSCVBSUOR
      UPUMUN $.
      $( [14-Nov-2013] $)

    $( The predicate "is a partial function." $)
    elpm2g $p |- ( ( A e. V /\ B e. W ) ->
                ( F e. ( A ^pm B ) <-> ( F : dom F --> A /\ dom F C_ B ) ) ) $=
      ( wcel wa cpm co wfun cxp wss cdm wf elpmg funssxp syl6bb ) ADFBEFGCABHIF
      CJCBAKLGCMZACNRBLGABCDEOBACPQ $.
      $( [31-Dec-2013] $)

    $( Sufficient condition for being a partial function. $)
    elpm2r $p |- ( ( ( A e. V /\ B e. W ) /\ ( F : C --> A /\ C C_ B ) ) ->
                   F e. ( A ^pm B ) ) $=
      ( wcel wa wf wss cpm co cdm wb fdm feq2d sseq1d anbi12d adantr ibir imp
      elpm2g syl5ibr ) AEGBFGHZCADIZCBJZHZDABKLGZUGUHUDDMZADIZUIBJZHZUGULUEULUG
      NUFUEUJUEUKUFUEUICADCADOZPUEUICBUMQRSTABDEFUBUCUA $.
      $( [31-Dec-2013] $)

    $( A partial function is a function.  (Contributed by Mario Carneiro,
       30-Jan-2014.) $)
    pmfun $p |- ( F e. ( A ^pm B ) -> Fun F ) $=
      ( cvv wcel cpm co wfun cxp wss wa c0 wceq wn n0i elpmg biimpcd simpl syl6
      wrel wi cdm fnpm fndm ax-mp ndmoprg mp3an1 ex con1d sylcom oprprc2 eleq2d
      wfn syl5 relxp releqi mpbir oprprc1 con1i syl mp2and syl6bi pm2.61i ) BDE
      ZCABFGZEZCHZUAVDVFVGCBAIJZKZVGVDVFADEZVDKZVIVFVELMZNVDVKVECOVDVKVLVDVKNZV
      LFUBZDDIZMZVDVMVLFVOUMVPUCVOFUDUEZABDDDFUFUGUHUIUNVKVFVIABCDDPQUJVGVHRSVD
      NZVFVGCAAIJZKZVGVRVFCAAFGZEZVTVRVEWACABFUKULWBVJVJVTWBWALMZNVJWACOVJWCAAF
      VNTVOTDDUOVNVOVQUPUQURUSUTZWDVJVJKWBVTAACDDPQVAVBVGVSRSVC $.
      $( [30-Jan-2014] $)
  $}

  $( A total function is a partial function. $)
  fpmg $p |- ( ( A e. V /\ B e. W /\ F : A --> B ) -> F e. ( B ^pm A ) ) $=
    ( wcel wf cpm co wa wss ssid elpm2r mpanr2 3impa 3com12 ) BEFZADFZABCGZCBAH
    IFZQRSTQRJSAAKTALBAACEDMNOP $.
    $( [31-Dec-2013] $)

  ${
    $d f A $.  $d f B $.  $d f C $.  $d f D $.  $d f V $.  $d f W $.
    $( Subset relation for the set of partial functions. $)
    pmss12g $p |- ( ( ( A C_ C /\ B C_ D ) /\ ( C e. V /\ D e. W ) ) ->
                   ( A ^pm B ) C_ ( C ^pm D ) ) $=
      ( vf wss wa wcel cpm co cv wfun cxp wi wb cvv ssexg elpmg xpss12 sstr syl
      ancoms expcom anim2d adantr syl2an an4s adantl 3imtr4d ssrdv ) ACHZBDHZIZ
      CEJZDFJZIZIZGABKLZCDKLZUSGMZNZVBBAOZHZIZVCVBDCOZHZIZVBUTJZVBVAJZUOVFVIPUR
      UOVEVHVCUOVDVGHZVEVHPUNUMVLBDACUAUDVEVLVHVBVDVGUBUEUCUFUGUMUPUNUQVJVFQZUM
      UPIARJBRJVMUNUQIACESBDFSABVBRRTUHUIURVKVIQUOCDVBEFTUJUKUL $.
      $( [31-Dec-2013] $)
  $}

  $( Elementhood of a restricted function in the set of partial functions. $)
  pmresg $p |- ( ( A e. V /\ B e. W /\ F e. ( A ^pm C ) ) ->
                 ( F |` B ) e. ( A ^pm B ) ) $=
    ( wcel cpm co cres wa wf wss cvv elpm2g simpl syl6bi ex wceq 3syl cdm wi wn
    cin oprprc2 eleq2d wb anidms sylan9bbr pm2.61d imp fssres sylancl wfun ffun
    inss1 resres funrel resdm reseq1 syl5eqr feq1d adantlr elpm2r mpanr2 syldan
    wrel mpbid inss2 3impa ) AEGZBFGZDACHIZGZDBJZABHIGZVKVLKZVNDUAZBUDZAVOLZVPV
    KVNVTVLVKVNKZVSADVSJZLZVTWAVRADLZVSVRMWCVKVNWDVKCNGZVNWDUBZVKWEWFVKWEKVNWDV
    RCMZKWDACDENOWDWGPQRVKWEUCZWFVKWHKVNWDVRAMZKZWDWHVNDAAHIZGZVKWJWHVMWKDACHUE
    UFVKWLWJUGAADEEOUHUIWDWIPQRUJUKZVRBUPVRAVSDULUMWAVSAWBVOWAWDDUNZWBVOSWMVRAD
    UOWNWBDVRJZBJZVODVRBUQWNDVGWODSWPVOSDURDUSWODBUTTVATVBVHVCVQVTVSBMVPVRBVIAB
    VSVOEFVDVEVFVJ $.
    $( [31-Dec-2013] $)

  ${
    $d g A $.  $d f g B $.  $d g F $.
    elmap.1 $e |- A e. _V $.
    elmap.2 $e |- B e. _V $.
    $( Membership relation for set exponentiation. $)
    elmap $p |- ( F e. ( A ^m B ) <-> F : B --> A ) $=
      ( cvv wcel cmap co wf wb elmapg mp2an ) AFGBFGCABHIGBACJKDEABCFFLM $.
      $( [8-Dec-2003] $)

    $( Alternate expression for the value of set exponentiation. $)
    mapval2 $p |- ( A ^m B ) = ( ~P ( B X. A ) i^i { f | f Fn B } ) $=
      ( vg cmap co cxp cpw cv wfn cab cin wf wss wa wcel crn bitri ffn jca rnss
      fssxp rnxpss sstr sylancl anim2i df-f sylibr impbii ancom elmap elin elpw
      vex fneq1 elab anbi12i 3bitr4i eqriv ) FABGHZBAIZJZCKZBLZCMZNZBAFKZOZVIVC
      PZVIBLZQZVIVBRVIVHRZVJVLVKQZVMVJVOVJVLVKBAVIUABAVIUDUBVOVLVISZAPZQVJVKVQV
      LVKVPVCSZPVRAPVQVIVCUCBAUEVPVRAUFUGUHBAVIUIUJUKVLVKULTABVIDEUMVNVIVDRZVIV
      GRZQVMVIVDVGUNVSVKVTVLVIVCFUPZUOVFVLCVIWABVEVIUQURUSTUTVA $.
      $( [3-Nov-2007] $)

    $( The predicate "is a partial function." $)
    elpm $p |- ( F e. ( A ^pm B ) <-> ( Fun F /\ F C_ ( B X. A ) ) ) $=
      ( cvv wcel cpm co wfun cxp wss wa wb elpmg mp2an ) AFGBFGCABHIGCJCBAKLMND
      EABCFFOP $.
      $( [14-Nov-2013] $) $( [15-Nov-2007] $)

    $( The predicate "is a partial function." $)
    elpm2 $p |- ( F e. ( A ^pm B ) <-> ( F : dom F --> A /\ dom F C_ B ) ) $=
      ( cvv wcel cpm co cdm wf wss wa wb elpm2g mp2an ) AFGBFGCABHIGCJZACKQBLMN
      DEABCFFOP $.
      $( [31-Dec-2013] $) $( [15-Nov-2007] $)

    $( A total function is a partial function. $)
    fpm $p |- ( F : A --> B -> F e. ( B ^pm A ) ) $=
      ( cvv wcel wf cpm co fpmg mp3an12 ) AFGBFGABCHCBAIJGDEABCFFKL $.
      $( [31-Dec-2013] $) $( [15-Nov-2007] $)

    $( Set exponentiation is a subset of partial maps. $)
    mapsspm $p |- ( A ^m B ) C_ ( A ^pm B ) $=
      ( vg cmap co cpm cv wcel wf elmap fpm sylbi ssriv ) EABFGZABHGZEIZPJBARKR
      QJABRCDLBARDCMNO $.
      $( [15-Nov-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x y R $.
    fvopabf4.1 $e |- C e. _V $.
    fvopabf4.2 $e |- D e. _V $.
    fvopabf4.3 $e |- R e. _V $.
    fvopabf4.4 $e |- ( x = A -> B = C ) $.
    fvopabf4.5 $e |- F = { <. x , y >. | ( x : D --> R /\ y = B ) } $.
    $( Special case of ~ fvopab4 for operator theorems. $)
    fvopabf4 $p |- ( A : D --> R -> ( F ` A ) = C ) $=
      ( wf wcel wceq elmap cv wa copab cmap co cfv anbi1i opabbii eqtr4i sylbir
      fvopab4 ) FGCNCGFUAUBZOCHUCEPGFCKJQABCDEUIHLHFGARZNZBRDPZSZABTUJUIOZULSZA
      BTMUOUMABUNUKULGFUJKJQUDUEUFIUHUG $.
      $( [27-Nov-2007] $)
  $}

  ${
    $d f A $.  $d f B $.
    $( Set exponentiation is a subset of the power set of the cross product of
       its arguments. $)
    mapsspw $p |- ( B e. R -> ( A ^m B ) C_ ~P ( B X. A ) ) $=
      ( vf cvv wcel cmap co cxp cpw wss wa cv wf cab mapvalg a1i eqsstrd wrel
      c0 fssxp ss2abi df-pw sseqtr4i cdm relxp wfn wceq fnmap fndm ax-mp releqi
      wn mpbir oprprc1 0ss adantr pm2.61ian ) AEFZBCFZABGHZBAIZJZKZUSUTLZVABADM
      ZNZDOZVCABECDPVHVCKVEVHVFVBKZDOVCVGVIDBAVFUAUBDVBUCUDQRUSUMZVDUTVJVATVCAB
      GGUEZSEEIZSEEUFVKVLGVLUGVKVLUHUIVLGUJUKULUNUOTVCKVJVCUPQRUQUR $.
      $( [8-Dec-2006] $)
  $}

  ${
    $d f A $.
    map0e.1 $e |- A e. _V $.
    $( Set exponentiation with an empty exponent (ordinal number 0) is ordinal
       number 1.  Exercise 4.42(a) of [Mendelson] p. 255. $)
    map0e $p |- ( A ^m (/) ) = 1o $=
      ( vf c0 cmap co csn c1o cv wf cab wceq wfn crn wss wa fn0 anbi1i df-f 0ss
      rn0 syl6eq sseq1d mpbiri pm4.71i 3bitr4i abbii mapval df-sn 3eqtr4i df1o2
      rneq 0ex eqtr4i ) ADEFZDGZHDACIZJZCKUQDLZCKUOUPURUSCUQDMZUQNZAOZPUSVBPURU
      SUTUSVBUQQRDAUQSUSVBUSVBDAOATUSVADAUSVADNDUQDULUAUBUCUDUEUFUGADCBUMUHCDUI
      UJUKUN $.
      $( [10-Dec-2003] $)

    $( Set exponentiation with an empty base is the empty set, provided the
       exponent is non-empty.  Theorem 96 of [Suppes] p. 89. $)
    map0b $p |- ( A =/= (/) -> ( (/) ^m A ) = (/) ) $=
      ( vf c0 wne cmap co cv cab 0ex mapval wex wceq abn0 cdm fdm crn wss frn
      wf ss0 syl dm0rn0 sylibr eqtr3d exlimiv sylbi necon1i syl5eq ) ADEDAFGADC
      HZTZCIZDDACJBKULDADULDEUKCLADMZUKCNUKUMCUKUJOZADADUJPUKUJQZDMZUNDMUKUODRU
      PADUJSUOUAUBUJUCUDUEUFUGUHUI $.
      $( [19-Mar-2007] $) $( [10-Dec-2003] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    map0.1 $e |- A e. _V $.
    map0.2 $e |- B e. _V $.
    $( Set exponentiation is empty iff the base is empty and the exponent is
       not empty.  Theorem 97 of [Suppes] p. 89. $)
    map0 $p |- ( ( A ^m B ) = (/) <-> ( A = (/) /\ B =/= (/) ) ) $=
      ( vf vx cmap co c0 wceq wne wa cv wf cab mapval eqeq1i wcel wex csn snssi
      wss cxp vex fconst fss mpan snex xpex feq1 cla4ev 3syl exlimiv n0 3imtr4i
      abn0 necon4i sylbi c1o map0e df1o2 eqtri 0ex eqnetri opreq2 neeq1d mpbiri
      snnz necon2i jca opreq1 map0b sylan9eq impbii ) ABGHZIJZAIJZBIKZLVPVQVRVP
      BAEMZNZEOZIJVQVOWAIABECDPQAIWAIFMZARZFSVTESZAIKWAIKWCWDFWCWBTZAUBZBABWEUC
      ZNZWDWBAUABWEWGNWFWHBWBFUDUEBWEAWGUFUGVTWHEWGBWEDWBUHUIBAVSWGUJUKULUMFAUN
      VTEUPUOUQURBIVOIBIJZVOIKAIGHZIKWJITZIWJUSWKACUTVAVBIVCVHVDWIVOWJIBIAGVEVF
      VGVIVJVQVRVOIBGHIAIBGVKBDVLVMVN $.
      $( [17-May-2007] $) $( [10-Dec-2003] $)

    $( The value of set exponentiation with a singleton exponent.  Theorem 98
       of [Suppes] p. 89. $)
    mapsn $p |- ( A ^m { B } ) = { f | E. y e. A f = { <. B , y >. } } $=
      ( cv cop csn wceq wrex cmap wcel wf wex snid cima syl mpbiri wf1o co snex
      wa crn wbr weu wfn ffn fneu sylancl cab euabsn wrel frel relimasn imadmrn
      elmap cdm fdm imaeq2d syl5reqr eqtr3d eqeq1d exbidv mpbid vex eleq2 sseld
      syl5bb frn syl5 wfo dffn4 sylib fof feq3 syl5ibcom fsn syl6ib jcad eximdv
      mpd df-rex sylibr wss f1osn f1oeq1 f1of fss syl2an expcom rexlimiv impbii
      snssi bitri abbi2i ) DGZCAGZHIZJZABKZDBCIZLUAZWQXCMXBBWQNZXABXBWQECUBUQXD
      XAXDWRBMZWTUCZAOZXAXDWQUDZWRIZJZAOZXGXDCWRWQUEZAUFZXKXDWQXBUGZCXBMXMXBBWQ
      UHZCFPAXBCWQUIUJXMXLAUKZXIJZAOXDXKXLAULXDXQXJAXDXPXHXIXDWQXBQZXPXHXDWQUMX
      RXPJXBBWQUNACWQUORXDXHWQWQURZQXRWQUPXDXSXBWQXBBWQUSUTVAVBVCVDVIVEXDXJXFAX
      DXJXEWTXJWRXHMZXDXEXJXTWRXIMWRAVFZPXHXIWRVGSXDXHBWRXBBWQVJVHVKXDXJXBXIWQN
      ZWTXDXBXHWQNZXJYBXDXBXHWQVLZYCXDXNYDXOXBWQVMVNXBXHWQVORXHXIXBWQVPVQCWRWQF
      YAVRVSVTWAWBWTABWCWDWTXDABWTXEXDWTYBXIBWEXDXEWTXBXIWQTZYBWTYEXBXIWSTCWRFY
      AWFXBXIWQWSWGSXBXIWQWHRWRBWNXBXIBWQWIWJWKWLWMWOWP $.
      $( [10-Dec-2003] $)
  $}

  ${
    $d f A $.  $d f B $.  $d f C $.
    mapss.1 $e |- B e. _V $.
    mapss.2 $e |- C e. _V $.
    $( Subset inheritance for set exponentiation.  Theorem 99 of [Suppes]
       p. 89. $)
    mapss $p |- ( A C_ B -> ( A ^m C ) C_ ( B ^m C ) ) $=
      ( vf wss cv wf cab cmap co fss expcom ss2abdv cvv wcel wceq ssex mapvalg
      sylancl mapval a1i 3sstr4d ) ABGZCAFHZIZFJZCBUFIZFJZACKLZBCKLZUEUGUIFUGUE
      UICABUFMNOUEAPQCPQUKUHRABDSEACPPFTUAULUJRUEBCFDEUBUCUD $.
      $( [10-Dec-2003] $)
  $}

