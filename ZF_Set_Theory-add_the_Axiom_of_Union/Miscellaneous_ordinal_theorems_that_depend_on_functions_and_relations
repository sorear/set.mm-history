$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
  Miscellaneous ordinal theorems (that depend on functions and relations)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.  $d y z B $.
    iunon.1 $e |- A e. V $.
    iunon.2 $e |- B e. V $.
    $( The indexed union of a set of ordinal numbers is an ordinal number.
       ` B ` normally has free variable ` x ` as a parameter. $)
    iunon $p |- ( A. x e. A B e. On -> U_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral cv wceq wrex cab cuni ciun wss wi wal hbra1 ax-17
      ra4 eleq1a syl6 r19.23ad abid syl5ib 19.21aiv hbab1 dfss2f sylibr
      abrexex ssonuni syl dfiun2 syl5eqel ) CHIZABJZFKZCLZABMZFNZOZHABCPURVBHQZ
      VCHIURUSVBIZUSHIZRZFSVDURVGFURVAVFVEURUTVFABUQABTVFAUAURAKBIUQUTVFRUQABUB
      CHUSUCUDUEVAFUFUGUHFGVBHVAFGUIGKHIFUAUJUKVBAFBCDULUMUNAFBCEUOUP $.
      $( [14-Oct-2003] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    iinon.1 $e |- B e. V $.
    $( The indexed intersection of a non-empty class of ordinal numbers is an
       ordinal number.  ` B ` normally has free variable ` x ` as a parameter.
       Note that ` A ` may be a proper class. $)
    iinon $p |- ( ( A. x e. A B e. On /\ A =/= (/) ) ->
                |^|_ x e. A B e. On ) $=
      ( vy vz con0 wcel wral c0 wne wa cv wceq wrex cab cint ciin wss oninton
      wex df-rex exbii excom 19.42v isseti mpbiran2 3bitr2r ne0 abn0 3bitr4
      sylan2b wi wal hbra1 ax-17 ra4 eleq1a syl6 r19.23ad abid syl5ib 19.21aiv
      hbab1 dfss2f sylibr sylan dfiin2 syl5eqel ) CGHZABIZBJKZLEMZCNZABOZEPZQZG
      ABCRVPGSZVLVQGHZVKVRVPJKZVSVLVPTAMBHZAUAZVOEUAZVLVTWCWAVNLZAUAZEUAWDEUAZA
      UAWBVOWEEVNABUBUCWDAEUDWFWAAWFWAVNEUAWAVNEUEECDUFUGUCUHABUIVOEUJUKULVKVMV
      PHZVMGHZUMZEUNVRVKWIEVKVOWHWGVKVNWHABVJABUOWHAUPVKWAVJVNWHUMVJABUQCGVMURU
      SUTVOEVAVBVCEFVPGVOEFVDFMGHEUPVEVFVGAEBCDVHVI $.
      $( [?] $) $( [13-Oct-2003] $)
  $}

