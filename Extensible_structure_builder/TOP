$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                    Extensible structure builder
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $c Struct $.

  $( Extend class notation with extensible structure builders. $)
  cstru $a class Struct ( N , f , ph ) $.

  ${
    $d m N $.  $d m f $.  $d m ph $.
    $( Define an extensible structure builder.  This device provides the
       ability to succesively specialize structures.  For example, a ring can
       be a specialization of a group, and all theorems for groups will
       automatically be applicable to rings.  In the intended applications,
       ` N ` is a positive integer and and ` ph ` is a wff containing ` f `
       free in the form of occurrences of ` ( f `` 1 ) ` ,...,
       ` ( f `` N ) ` .  Note that ` f ` is a bound variable in
       ` Struct ( N , f , ph ) ` . $)
    df-struct $a |- Struct ( N , f , ph ) = { f | ( E. m e. NN ( N <_ m /\
          f Fn ( 1 ... m ) ) /\ ph ) } $.

  $}

  ${
    $d m n N $.  $d m n f $.  $d n ph $.
    $( Version of ~ df-struct with distinct variable requirement of ` m ` and
       ` ph ` removed. $)
    dfstruct2 $p |- Struct ( N , f , ph ) = { f | ( E. m e. NN ( N <_ m /\
          f Fn ( 1 ... m ) ) /\ ph ) } $=
      ( vn cstru cv cle wbr c1 cfz co wfn wa cn wrex cab df-struct wceq breq2
      opreq2 fneq2d anbi12d cbvrexv anbi1i abbii eqtri ) ABDFDEGZHIZBGZJUHKLZMZ
      NZEOPZANZBQDCGZHIZUJJUPKLZMZNZCOPZANZBQABEDRUOVBBUNVAAUMUTECOUHUPSZUIUQUL
      USUHUPDHTVCUKURUJUHUPJKUAUBUCUDUEUFUG $.
      $( [30-Aug-2011] $)

  $}

  ${
    $d m N $.  $d m f x $.
    $( Bound-variable hypothesis builder for an extensible structure
       builder. $)
    hbstr1 $p |- ( x e. Struct ( N , f , ph )
           -> A. f x e. Struct ( N , f , ph ) ) $=
      ( vm cstru cv cle wbr c1 cfz co wfn wa cn wrex cab dfstruct2 hbab1 hbxfr
      ) CBACDFDEGZHICGJUAKLMNEOPANZCQACEDRUBCBST $.
      $( [31-Aug-2011] $)
  $}

  ${
    $d m N $.  $d m f $.
    strss.1 $e |- ( ph -> ps ) $.
    $( Subclass relation for extensible structure builder. $)
    strss $p |- Struct ( N , f , ph ) C_ Struct ( N , f , ps ) $=
      ( vm cv cle wbr c1 cfz co wfn wa cn wrex cab cstru anim2i ss2abi
      dfstruct2 3sstr4i ) DFGZHICGJUCKLMNFOPZANZCQUDBNZCQACDRBCDRUEUFCABUDESTAC
      FDUABCFDUAUB $.
      $( [5-Sep-2011] $)
  $}

  ${
    strbi.1 $e |- ( ph <-> ps ) $.
    $( Equality theorem for extensible structure builder. $)
    strbi $p |- Struct ( N , f , ph ) = Struct ( N , f , ps ) $=
      ( cstru biimpi strss biimpri eqssi ) ACDFBCDFABCDABEGHBACDABEIHJ $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d m f N $.  $d m ph $.
    $( Th. 8.3.77 p. 201 $)
    $( The specialization of an extensible structure builder generates a
       subclass. $)
    strssp1 $p |- ( N e. NN -> Struct ( ( N + 1 ) , f , ( ph /\ ps ) )
                    C_ Struct ( N , f , ph ) ) $=
      ( vm cn wcel c1 caddc co cv cle wbr cfz wfn wa wrex cab cstru cr wi w3a
      lep1 3ad2ant1 letr mpand 3expa nnre peano2re ancli syl syl2an anim1d
      reximdva simpl a1i anim12d ss2abdv dfstruct2 3sstr4g ) DFGZDHIJZEKZLMZCKH
      VCNJOZPZEFQZABPZPZCRDVCLMZVEPZEFQZAPZCRVHCVBSACDSVAVIVMCVAVGVLVHAVAVFVKEF
      VAVCFGZPVDVJVEDTGZVBTGZPZVCTGZVDVJUAZVAVNVOVPVRVSVOVPVRUBDVBLMZVDVJVOVPVT
      VRDUCUDDVBVCUEUFUGVAVOVQDUHVOVPDUIUJUKVCUHULUMUNVHAUAVAABUOUPUQURVHCEVBUS
      ACEDUSUT $.
      $( [29-Jul-2011] $)
  $}

  ${
    $d f m F $.  $d f m N $.  $d f ps $.
    elstr.1 $e |- ( f = F -> ( ph <-> ps ) ) $.
    $( Membership in an extensible structure builder. $)
    elstr $p |- ( F e. Struct ( N , f , ph ) <->
                      ( E. m e. NN ( N <_ m /\ F Fn ( 1 ... m ) ) /\ ps ) ) $=
      ( cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex cab dfstruct2 eleq2i
      cvv oprex fnex mpan2 ad2antll r19.23aiva adantr wceq fneq1 anbi2d
      rexbidv anbi12d elab3 bitri ) EACFHZIEFDJZKLZCJZMURNOZPZQZDRSZAQZCTZIUSEV
      APZQZDRSZBQZUQVFEACDFUAUBVEVJCEVIEUCIZBVHVKDRVGVKURRIUSVGVAUCIVKMURNUDVAU
      CEUEUFUGUHUIUTEUJZVDVIABVLVCVHDRVLVBVGUSVAUTEUKULUMGUNUOUP $.
      $( [12-Aug-2011] $)

    $( Implication of membership in an extensible structure builder. $)
    elstri $p |- ( F e. Struct ( N , f , ph ) -> ps ) $=
      ( vm cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex elstr simprbi ) DACEH
      IEGJZKLDMUBNOPQGRSBABCGDEFTUA $.
      $( [12-Aug-2011] $)
  $}

  ${
    $d f m F $.  $d f m N $.
    elstrscaf.1 $e |- S = Struct ( N , f , T. ) $.
    $( Membership in the "scaffold" of a structure. $)
    elstrscaf $p |- ( F e. S <-> E. m e. NN ( N <_ m /\ F Fn ( 1 ... m ) ) ) $=
      ( wtru cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex wceq biidd elstr
      eleq2i tru biantru 3bitr4i ) DGBEHZIECJZKLDMUHNOPQCRSZGQDAIUIGGBCDEBJDTGU
      AUBAUGDFUCGUIUDUEUF $.
      $( [1-Sep-2011] $)
  $}

  ${
    $d f m F $.  $d f m N $.
    $( A finite sequence is a member of the "scaffold" of a structure. $)
    fnelstr $p |- ( ( N e. NN /\ F Fn ( 1 ... N ) )
                     -> F e. Struct ( N , f , T. ) ) $=
      ( vm cn wcel c1 cfz co wfn wa cv cle wbr wrex wtru cstru cr nnre leid
      syl wceq breq2 opreq2 fneq2d anbi12d rcla4ev ex mpand imp eqid elstrscaf
      sylibr ) CEFZBGCHIZJZKCDLZMNZBGUQHIZJZKZDEOZBPACQZFUNUPVBUNCCMNZUPVBUNCRF
      VDCSCTUAUNVDUPKZVBVAVEDCEUQCUBZURVDUTUPUQCCMUCVFUSUOBUQCGHUDUEUFUGUHUIUJV
      CADBCVCUKULUM $.
      $( [3-Sep-2011] $)
  $}

  ${
    $d f m F $.  $d f m N $.  $d f ps $.
    elstr2.1 $e |- S = Struct ( N , f , T. ) $.
    elstr2.2 $e |- ( f = F -> ( ph <-> ps ) ) $.
    $( Membership in an extensible structure builder that does not reference
       its explicit scaffold. $)
    elstr2 $p |- ( F e. Struct ( N , f , ph ) <-> ( F e. S /\ ps ) ) $=
      ( vm cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex elstr elstrscaf
      anbi1i bitr4i ) EADFJKFILZMNEOUFPQRSITUAZBSECKZBSABDIEFHUBUHUGBCDIEFGUCUD
      UE $.
      $( [30-Aug-2011] $)
  $}

  ${
    $d f m x N $.  $d x ph $.
    $( Removing structures built for for ` N + 1 ` leaves only functions on
       ` ( 1 ... N ) ` . $)
    strdif $p |- ( N e. NN
          -> ( Struct ( N , f , ph ) \ Struct ( ( N + 1 ) , f , ph ) )
                      = { f | ( f Fn ( 1 ... N ) /\ ph ) } ) $=
      ( vm vx cn wcel cv cstru c1 caddc co cdif cfz wfn wa cab wb wal wceq cle
      wbr wrex wn wi wral cr letri3 nnre syl2an clt lenlt ancoms nnltp1le
      notbid bitrd anbi2d eqcom syl5bb anbi1d con2b anbi2i anass an12 abai
      ancom 3bitr3i 3bitri syl6rbbr rexbidva opreq2 fneq2d ceqsrexv r19.29r
      syl5bi leid syl anim1i breq2 anbi12d rcla4ev syldan cuz cfv fzopth
      elnnuz syl2anb simplbda ad2antrr eqbrtrrd adantr mpbid fndmu sylan2 expr
      an1rs con2d r19.21aiva jca ex impbid ralinexa syl5bbr eldif dfstruct2
      abeq2i notbii imnan 3bitr2i anbi12i bitri abid 3bitr4g 19.21aiv hbstr1
      hbdif hbab1 cleqf sylibr ) CFGZBHZABCIZABCJKLZIZMZGZYAYAJCNLZOZAPZBQZGZRZ
      BSYEYJTXTYLBXTCDHZUAUBZYAJYMNLZOZPZDFUCZYCYMUAUBZYPPDFUCZUDZPZAPZYIYFYKXT
      UUBYHAXTYRYSYPUDUEZDFUFZPZYHUUBXTUUFYHXTYQUUDPZDFUCZYHUUFXTUUHYMCTZYPPZDF
      UCYHXTUUGUUJDFXTYMFGZPZUUJYNYSUDZPZYPPZUUGUULUUIUUNYPUULCYMTZUUNUUIUULUUP
      YNYMCUAUBZPZUUNCUGGZYMUGGZUUPUURRXTUUKCYMUHCUIZYMUIZUJUULUUQUUMYNUULUUQCY
      MUKUBZUDZUUMUUKXTUUQUVDRZUUTUUSUVEUUKXTYMCULUVBUVAUJUMUULUVCYSCYMUNUOUPZU
      QUPYMCURUSUTUUGYQYPUUMUEZPYNYPUVGPZPZUUOUUDUVGYQYSYPVAVBYNYPUVGVCYNYPUUMP
      ZPYPUUNPUVIUUOYNYPUUMVDUVJUVHYNYPUUMVEVBYPUUNVFVGVHVIVJYPYHDCFUUIYOYGYAYM
      CJNVKVLZVMUPYQUUDDFVNVOXTYHUUFXTYHPZYRUUEXTYHCCUAUBZYHPZYRXTUVMYHXTUUSUVM
      UVACVPVQZVRYQUVNDCFUUIYNUVMYPYHYMCCUAVSUVKVTWAWBUVLUUDDFUVLUUKPYPYSXTUUKY
      HUVGUULYHYPUUMUULYGYOTZUUMYHYPPUULUVPPZUUQUUMUVQCYMCUAUULUVPJJTZUUPCJWCWD
      ZGYMUVSGUVPUVRUUPPRXTUUKUVSJYMJCWECWFYMWFWGWHXTUVMUUKUVPUVOWIWJUULUUQUUMR
      UVPUVFWKWLYGYOYAWMWNWOWPWQWRWSWTXAUUEUUAYRYSYPDFXBVBXCUTYFYAYBGZYAYDGZUDZ
      PYRAPZAUUAUEZPZUUCYAYBYDXDUVTUWCUWBUWDUWCBYBABDCXEXFUWBYTAPZUDYTAUDUEUWDU
      WAUWFUWFBYDABDYCXEXFXGYTAXHYTAVAXIXJUWEYRAUWDPZPYRAUUAPZPZUUCYRAUWDVCUWHU
      WGYRAUUAVEVBUWIAUUBPUUCYRAUUAVDAUUBVFXKXIVHYIBXLXMXNBEYEYJBEYBYDAEBCXOAEB
      YCXOXPYIBEXQXRXS $.
      $( [31-Aug-2011] $)
  $}

  ${
    $d x F $.  $d f x N $.
    elstrdiff.1 $e |- ( x e. F -> A. f x e. F ) $.
    elstrdiff.2 $e |- ( ps -> A. f ps ) $.
    elstrdiff.3 $e |- ( f = F -> ( ph <-> ps ) ) $.
    $( Membership in an extensible structure builder for ` N ` but not
       ` N + 1 ` . $)
    elstrdiff $p |- ( N e. NN -> ( F e. ( Struct ( N , f , ph )
       \ Struct ( ( N + 1 ) , f , ph ) ) <-> ( F Fn ( 1 ... N ) /\ ps ) ) ) $=
      ( cn wcel cstru c1 caddc co cdif cv cfz wfn wa cab strdif eleq2d cvv wi
      wb oprex fnex mpan2 adantr ax-17 hbfn hban wceq fneq1 anbi12d elab3gf
      ax-mp syl6bb ) FJKZEADFLADFMNOLPZKEDQZMFROZSZATZDUAZKZEVCSZBTZUTVAVFEADFU
      BUCVIEUDKZUEVGVIUFVHVJBVHVCUDKVJMFRUGVCUDEUHUIUJVEVIDCEUDGVHBDDCVCEGCQVCK
      DUKULHUMVBEUNVDVHABVCVBEUOIUPUQURUS $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d f x F $.  $d f x N $.  $d f ps $.
    elstrdif.1 $e |- ( f = F -> ( ph <-> ps ) ) $.
    $( Membership in an extensible structure builder for ` N ` but not
       ` N + 1 ` . $)
    elstrdif $p |- ( N e. NN -> ( F e. ( Struct ( N , f , ph )
       \ Struct ( ( N + 1 ) , f , ph ) ) <-> ( F Fn ( 1 ... N ) /\ ps ) ) ) $=
      ( vx cv wcel ax-17 elstrdiff ) ABGCDEGHDICJBCJFK $.
      $( [31-Aug-2011] $)
  $}

  ${
    $d f g A $.  $d f g m N $.  $d g ph $.
    eustrdif.x $e |- A e. _V $.
    eustrdif.1 $e |- ( ph <-> A. m e. ( 1 ... N ) ( f ` m ) = A ) $.
    $( Condition implying a unique member in an extensible structure builder
       difference. $)
    eustrdif $p |- ( N e. NN -> E! f f e. ( Struct ( N , f , ph )
       \ Struct ( ( N + 1 ) , f , ph ) ) ) $=
      ( vg cn wcel cv cstru c1 caddc co cdif weu cfz wfn cfv wceq wral wa
      oprex eufnfv fveq1 eqeq1d ralbidv syl5bb elstrdif eubidv mpbiri hbstr1
      hbdif ax-17 eleq1 cbveu sylib ) EIJZHKZACELZACEMNOZLZPZJZHQZCKZVDJZCQUSVF
      UTMEROZSDKZUTTZBUAZDVIUBZUCZHQDVIBHMERUDFUEUSVEVNHAVMCUTEVGUTUAZVJVGTZBUA
      ZDVIUBVMAVOVQVLDVIVOVPVKBVJVGUTUFUGUHGUIUJUKULVEVHHCCHVAVCAHCEUMAHCVBUMUN
      VHHUOUTVGVDUPUQUR $.
      $( [31-Aug-2011] $)
  $}

  ${
    $d f x A $.  $d f x B $.
    eustrdif2.x $e |- A e. _V $.
    eustrdif2.y $e |- B e. _V $.
    eustrdif2.1 $e |- ( ph <-> ( ( f ` 1 ) = A /\ ( f ` 2 ) = B ) ) $.
    $( Condition implying a unique member in a 2-member extensible structure
       builder difference. $)
    eustrdif2 $p |- E! f f e. ( Struct ( 2 , f , ph )
       \ Struct ( ( 2 + 1 ) , f , ph ) ) $=
      ( vx c2 cn wcel cv cstru c1 caddc co cdif weu 2nn wceq cif ifex cfv wa
      cfz wral fzprval bitr4i eustrdif ax-mp ) IJKDLZADIMADINOPMQKDRSAHLZNTZBCU
      AZDHIUMBCEFUBANUKUCBTIUKUCCTUDULUKUCUNTHNIUEPUFGHBCUKUGUHUIUJ $.
      $( [6-Sep-2011] $)
  $}

  $c StrBldr $.

  $( Extend class notation with constructed structure. $)
  ccstr $a class StrBldr ( N , f , ph ) $.

  ${
    $( Define a constructed structure, which is a specific member of a
       ` Struct ` class ( ~ df-struct ). $)
    df-strbldr $a |- StrBldr ( N , f , ph ) = ( iota f
      f e. ( Struct ( N , f , ph ) \ Struct ( ( N + 1 ) , f , ph ) ) ) $.

  $}

  ${
    stbbi.1 $e |- ( ph <-> ps ) $.
    $( Equality theorem for a constructed structure. $)
    stbbi $p |- StrBldr ( N , f , ph ) = StrBldr ( N , f , ps ) $=
      ( cv cstru c1 caddc co cdif wcel cio ccstr wb wceq iotabi strbi difeq12i
      eleq2i mpg df-strbldr 3eqtr4i ) CFZACDGZACDHIJZGZKZLZCMZUDBCDGZBCUFGZKZLZ
      CMZACDNBCDNUIUNOUJUOPCUIUNCQUHUMUDUEUKUGULABCDERABCUFERSTUAACDUBBCDUBUC
      $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d f x $.
    $( Bound-variable hypothesis builder for a constructed structure. $)
    hbstb1 $p |- ( x e. StrBldr ( N , f , ph )
           -> A. f x e. StrBldr ( N , f , ph ) ) $=
      ( ccstr cv cstru c1 caddc co cdif wcel cio df-strbldr hbiota1 hbxfr ) CBA
      CDECFACDGACDHIJGKLZCMACDNQCBOP $.
      $( [6-Sep-2011] $)
  $}

  $( A constructed structure is a set. $)
  stbex $p |- StrBldr ( N , f , ph ) e. _V $=
    ( ccstr cv cstru c1 caddc co cdif wcel cio cvv df-strbldr iotaex eqeltri )
    ABCDBEABCFABCGHIFJKZBLMABCNQBOP $.
    $( [6-Sep-2011] $)

  ${
    $d f A $.  $d f m x N $.  $d x ph $.
    stbcl.1 $e |- A e. _V $.
    stbcl.2 $e |- ( ph <-> A. m e. ( 1 ... N ) ( f ` m ) = A ) $.
    stbcl.3 $e |- F = StrBldr ( N , f , ph ) $.
    $( A constructed structure belongs to its structure class. $)
    stbel $p |- ( N e. NN -> F e. ( Struct ( N , f , ph )
                      \ Struct ( ( N + 1 ) , f , ph ) ) ) $=
      ( vx cn wcel cv cstru c1 caddc co cdif cio weu eustrdif cab iotacl
      hbstr1 hbdif abid2f syl6eleq syl ccstr df-strbldr eqtri syl5eqel ) FKLZCM
      ACFNZACFOPQZNZRZLZCSZUQEUMURCTZUSUQLABCDFGHUAUTUSURCUBUQURCUCCJUQCJUNUPAJ
      CFUDAJCUOUDUEUFUGUHEACFUIUSIACFUJUKUL $.
      $( [6-Sep-2011] $)

    $( A constructed structure belongs to its structure scaffold. $)
    stbcl $p |- ( N e. NN -> F e. Struct ( N , f , T. ) ) $=
      ( cn wcel cstru c1 caddc co cdif wtru stbel difss tru a1i strss sstri
      sseli syl ) FJKEACFLZACFMNOLZPZKEQCFLZKABCDEFGHIRUHUIEUHUFUIUFUGSAQCFQATU
      AUBUCUDUE $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d f x A $.  $d m B $.  $d m x F $.  $d m M $.  $d f m x N $.
    stbval.1 $e |- A e. _V $.
    stbval.2 $e |- ( ph <-> A. m e. ( 1 ... N ) ( f ` m ) = A ) $.
    stbval.3 $e |- F = StrBldr ( N , f , ph ) $.
    stbval.4 $e |- ( m = M -> A = B ) $.
    $( Value of a constructed structure. $)
    stbval $p |- ( ( N e. NN /\ M e. NN /\ M <_ N ) -> ( F ` M ) = B ) $=
      ( vx cn wcel cle wbr w3a c1 cfz co cv cfv wceq wral wa 3simpc wb fznn
      3ad2ant1 mpbird wfn cstru caddc cdif ccstr biid stbbi stbel syl5eqel
      hbstb1 hbxfr ax-17 hbfv hbeq hbral fveq1 eqeq1d ralbidv elstrdiff mpbid
      simprd fveq2 eqeq12d rcla4v sylc ) HNOZGNOZGHPQZRZGSHTUAZOZEUBZFUCZBUDZEW
      AUEZGFUCZCUDZVTWBVRVSUFZVQVRVSUGVQVRWBWIUHVSGHUIUJUKVQVRWFVSVQFWAULZWFVQF
      WCDUBZUCZBUDZEWAUEZDHUMWNDHSUNUAUMUOZOWJWFUFVQADHUPZWOFWNBDEWPHIWNUQAWNDH
      JURUSKUTWNWFMDFHDMFWPKAMDHVAVBZWEDEWAWCWAODVCDMMWDBDMWCFWQMUBZWCODVCVDWRB
      ODVCVEVFWKFUDZWMWEEWAWSWLWDBWCWKFVGVHVIVJVKVLUJWEWHEGWAWCGUDWDWGBCWCGFVML
      VNVOVP $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d g C $.  $d g F $.  $d f g N $.
    strcval.1 $e |- F e. _V $.
    strcval.c $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    $( Value of a (defined) structure component.  Normally, ` C ` is a defined
       constant symbol such as ` base ` and ` N ` is a fixed integer such as
       ` 1 ` . $)
    strcval $p |- ( C ` F ) = ( F ` N ) $=
      ( vg cv cfv wceq fveq2 fveq1 eqeq12d cvv wcel visset fvex fvmpt ax-mp
      vtocl ) GHZAIZDUAIZJZCAIZDCIZJGCEUACJUBUEUCUFUACAKDUACLMUANOUDGPBUADBHZIU
      CNADUGUALFDUAQRST $.
      $( [9-Sep-2011] $)
  $}

  ${
    $d f m A $.  $d f m B $.  $d m F $.  $d m ph $.
    stb2cl.1 $e |- A e. _V $.
    stb2cl.2 $e |- B e. _V $.
    stb2cl.3 $e |- ( ph <-> ( ( f ` 1 ) = A /\ ( f ` 2 ) = B ) ) $.
    stb2cl.4 $e |- F = StrBldr ( 2 , f , ph ) $.
    $( A 2-member constructed structure belongs to its structure scaffold.
       Note that ` Struct ( 2 , f , T. ) ` is the class of all finite sequences
       with at least 2 members. $)
    stb2el $p |- F e. ( Struct ( 2 , f , ph )
                      \ Struct ( ( 2 + 1 ) , f , ph ) ) $=
      ( vm c2 cn wcel cstru c1 caddc co cdif 2nn cv wceq cif ifex cfv wa cfz
      wral fzprval bitr4i stbel ax-mp ) KLMEADKNADKOPQNRMSAJTZOUAZBCUBZDJEKUMBC
      FGUCAODTZUDBUAKUOUDCUAUEULUOUDUNUAJOKUFQUGHJBCUOUHUIIUJUK $.
      $( [6-Sep-2011] $)

    $( A 2-member constructed structure belongs to its structure scaffold.
       Note that ` Struct ( 2 , f , T. ) ` is the class of all finite sequences
       with at least 2 members. $)
    stb2cl $p |- F e. Struct ( 2 , f , T. ) $=
      ( vm c2 cn wcel wtru cstru 2nn cv c1 wceq cif ifex cfv wa cfz co wral
      fzprval bitr4i stbcl ax-mp ) KLMENDKOMPAJQZRSZBCTZDJEKULBCFGUAARDQZUBBSKU
      NUBCSUCUKUNUBUMSJRKUDUEUFHJBCUNUGUHIUIUJ $.
      $( [6-Sep-2011] $)

    $( Value of a 2-member constructed structure. $)
    stb2val1 $p |- ( F ` 1 ) = A $=
      ( vm c2 cn wcel c1 cle wbr cfv wceq 2nn 1nn 1re 2re 1lt2 ltleii cv cif
      ifex wa cfz co wral fzprval bitr4i iftrue stbval mp3an ) KLMNLMNKOPNEQBRS
      TNKUAUBUCUDAJUEZNRZBCUFZBDJENKURBCFGUGANDUEZQBRKUTQCRUHUQUTQUSRJNKUIUJUKH
      JBCUTULUMIURBCUNUOUP $.
      $( [6-Sep-2011] $)

    $( Value of a 2-member constructed structure. $)
    stb2val2 $p |- ( F ` 2 ) = B $=
      ( vm c2 cn wcel cle wbr cfv wceq 2nn 2re leidi cv c1 cif ifex wa cfz co
      wral fzprval bitr4i wn wne clt 1lt2 1re ltnei ax-mp pm13.181 mpan2 df-ne
      sylib iffalse syl stbval mp3an ) KLMZVFKKNOKEPCQRRKSTAJUAZUBQZBCUCZCDJEKK
      VHBCFGUDAUBDUAZPBQKVJPCQUEVGVJPVIQJUBKUFUGUHHJBCVJUIUJIVGKQZVHUKZVICQVKVG
      UBULZVLVKKUBULZVMUBKUMOVNUNUBKUOSUPUQVGKUBURUSVGUBUTVAVHBCVBVCVDVE $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d f m A $.  $d f m B $.  $d f m C $.  $d m F $.  $d m ph $.
    stb3cl.1 $e |- A e. _V $.
    stb3cl.2 $e |- B e. _V $.
    stb3cl.c $e |- C e. _V $.
    stb3cl.3 $e |- ( ph
                  <-> ( ( f ` 1 ) = A /\ ( f ` 2 ) = B /\ ( f ` 3 ) = C ) ) $.
    stb3cl.4 $e |- F = StrBldr ( 3 , f , ph ) $.
    $( A 3-member constructed structure belongs to its structure scaffold.
       Note that ` Struct ( 3 , f , T. ) ` is the class of all finite sequences
       with at least 3 members. $)
    stb3el $p |- F e. ( Struct ( 3 , f , ph )
                      \ Struct ( ( 3 + 1 ) , f , ph ) ) $=
      ( vm c3 cn wcel cstru c1 caddc co cdif 3nn cv wceq c2 cif ifex cfv w3a
      cfz wral fztpval bitr4i stbel ax-mp ) MNOFAEMPAEMQRSPTOUAALUBZQUCZBUOUDUC
      ZCDUEZUEZELFMUPBURGUQCDHIUFUFAQEUBZUGBUCUDUTUGCUCMUTUGDUCUHUOUTUGUSUCLQMU
      ISUJJLBCDUTUKULKUMUN $.
      $( [16-Sep-2011] $)

    $( A 2-member constructed structure belongs to its structure scaffold.
       Note that ` Struct ( 3 , f , T. ) ` is the class of all finite sequences
       with at least 3 members. $)
    stb3cl $p |- F e. Struct ( 3 , f , T. ) $=
      ( vm c3 cn wcel wtru cstru 3nn cv c1 wceq c2 cif ifex cfv w3a cfz co
      wral fztpval bitr4i stbcl ax-mp ) MNOFPEMQORALSZTUAZBUNUBUAZCDUCZUCZELFMU
      OBUQGUPCDHIUDUDATESZUEBUAUBUSUECUAMUSUEDUAUFUNUSUEURUALTMUGUHUIJLBCDUSUJU
      KKULUM $.
      $( [16-Sep-2011] $)

    $( Value of a 3-member constructed structure. $)
    stb3val1 $p |- ( F ` 1 ) = A $=
      ( vm c3 cn wcel c1 cle wbr cfv wceq 3nn 1nn 1re 3re 1lt3 ltleii cv c2
      cif ifex w3a cfz co wral fztpval bitr4i iftrue stbval mp3an ) MNOPNOPMQRP
      FSBTUAUBPMUCUDUEUFALUGZPTZBUTUHTZCDUIZUIZBELFPMVABVCGVBCDHIUJUJAPEUGZSBTU
      HVESCTMVESDTUKUTVESVDTLPMULUMUNJLBCDVEUOUPKVABVCUQURUS $.
      $( [16-Sep-2011] $)

    $( Value of a 3-member constructed structure. $)
    stb3val2 $p |- ( F ` 2 ) = B $=
      ( vm c3 cn wcel c2 cle wbr cfv wceq 3nn 2nn 2re 3re 2lt3 ltleii cv c1
      cif ifex w3a cfz co wral fztpval bitr4i wn wne clt 1lt2 1re ltnei ax-mp
      pm13.181 mpan2 df-ne sylib iffalse syl iftrue eqtrd stbval mp3an ) MNOPNO
      PMQRPFSCTUAUBPMUCUDUEUFALUGZUHTZBVNPTZCDUIZUIZCELFPMVOBVQGVPCDHIUJUJAUHEU
      GZSBTPVSSCTMVSSDTUKVNVSSVRTLUHMULUMUNJLBCDVSUOUPKVPVRVQCVPVOUQZVRVQTVPVNU
      HURZVTVPPUHURZWAUHPUSRWBUTUHPVAUCVBVCVNPUHVDVEVNUHVFVGVOBVQVHVIVPCDVJVKVL
      VM $.
      $( [16-Sep-2011] $)

    $( Value of a 3-member constructed structure. $)
    stb3val3 $p |- ( F ` 3 ) = C $=
      ( vm c3 cn wcel cle wbr cfv wceq 3nn 3re leidi cv c1 c2 cif ifex w3a cfz
      co wral fztpval bitr4i wn wne clt 1lt3 1re ltnei ax-mp pm13.181 mpan2
      df-ne sylib iffalse syl 2lt3 2re eqtrd stbval mp3an ) MNOZVLMMPQMFRDSTTMU
      AUBALUCZUDSZBVMUESZCDUFZUFZDELFMMVNBVPGVOCDHIUGUGAUDEUCZRBSUEVRRCSMVRRDSU
      HVMVRRVQSLUDMUIUJUKJLBCDVRULUMKVMMSZVQVPDVSVNUNZVQVPSVSVMUDUOZVTVSMUDUOZW
      AUDMUPQWBUQUDMURUAUSUTVMMUDVAVBVMUDVCVDVNBVPVEVFVSVOUNZVPDSVSVMUEUOZWCVSM
      UEUOZWDUEMUPQWEVGUEMVHUAUSUTVMMUEVAVBVMUEVCVDVOCDVEVFVIVJVK $.
      $( [16-Sep-2011] $)
  $}

  ${
    $d m F $.  $d m S $.  $d m T $.  $d f m X $.  $d f m Y $.  $d f h m $.
    $d g h m $.
    stb2xpl.s $e |- S = ( g e. _V |-> ( g ` 1 ) ) $.
    stb2xpl.t $e |- T = ( g e. _V |-> ( g ` 2 ) ) $.
    stb2xpl.3 $e |- X e. _V $.
    stb2xpl.4 $e |- Y e. _V $.
    stb2xpl.5 $e |- F
                 = StrBldr ( 2 , f , ( ( S ` f ) = X /\ ( T ` f ) = Y ) ) $.
    $( The explicit equivalent of a constructed 2-member structure.  This
       theorem lets us go back and forth between constructed and explicit
       representations. $)
    stb2xpl $p |- F = { <. 1 , X >. , <. 2 , Y >. } $=
      ( vm vh c1 cop c2 cpr wceq cv cfv cfz co wral cif wa wcel wo caddc
      visset elpr df-2 opreq2i cz 1z fzpr ax-mp eqtri eleq2i eqeq2i orbi2i
      3bitr4i wne clt wbr 1lt2 1re 2re ltnei necom mpbi cr elisseti fvpr1
      fveq2 iftrue 3eqtr4a fvpr2 wn pm13.181 mpan2 df-ne sylib iffalse syl
      jaoi sylbi eqeq2d ralbiia fzprval bitri strcval eqeq1i anbi12i stb2val1
      stb2val2 mpbir2an wfn wb cstru cdif stb2el cn 2nn ccstr hbstb1 hbxfr cvv
      cmpt ax-17 fveq1 cbvmpt 3eqtri hbmpt1 hbfv hbeq hban eqeq1d anbi12d
      elstrdiff simpli wf fpr ffn preq2 3eqtr4i fneq2i mpbir eqfnfv2 mp2an ) EO
      FPQGPRZSZMTZEUAZYMYKUAZSZMOQUBUCZUDZYROEUAFSZQEUAGSZYRYNYMOSZFGUEZSZMYQUD
      YSYTUFYPUUCMYQYMYQUGZYOUUBYNUUDUUAYMQSZUHZYOUUBSZYMOOOUIUCZRZUGUUAYMUUHSZ
      UHUUDUUFYMOUUHMUJUKYQUUIYMYQOUUHUBUCZUUIQUUHOUBULUMZOUNUGUUKUUISUOOUPUQZU
      RUSUUEUUJUUAQUUHYMULUTVAVBUUAUUGUUEUUAOYKUAZFYOUUBOQVCZUUNFSQOVCZUUOOQVDV
      EUUPVFOQVGVHVIUQZQOVJVKZOQFGOVLVGVMZQVLVHVMZJKVNUQYMOYKVOUUAFGVPVQUUEQYKU
      AZGYOUUBUUOUVAGSUUROQFGUUSUUTJKVRUQYMQYKVOUUEUUAVSZUUBGSUUEYMOVCZUVBUUEUU
      PUVCUUQYMQOVTWAYMOWBWCUUAFGWDWEVQWFWGWHWIMFGEWJWKCTZAUAZFSZUVDBUAZGSZUFZF
      GCEJKUVFOUVDUAZFSUVHQUVDUAZGSUVEUVJFADUVDOCUJZHWLWMUVGUVKGBDUVDQUVLIWLWMW
      NZLWOUVIFGCEJKUVMLWPWQEYQWRZYKYQWRZYLYRWSUVNEAUAZFSZEBUAZGSZUFZEUVICQWTUV
      ICQOUIUCWTXAUGZUVNUVTUFZUVIFGCEJKUVMLXBQXCUGUWAUWBWSXDUVIUVTMCEQCMEUVICQX
      ELUVIMCQXFXGZUVQUVSCCMMUVPFCMEACMACXHUVJXIZADXHODTZUAZXINXHONTZUAZXIUWDHD
      NMXHUWFUWHYMUWFUGNXJYMUWHUGZDXJOUWEUWGXKXLNCMXHUWHUVJUWICXJYMUVJUGNXJOUWG
      UVDXKXLXMCMXHUVJXNXGUWCXOYMFUGCXJXPCMMUVRGCMEBCMBCXHUVKXIZBDXHQUWEUAZXINX
      HQUWGUAZXIUWJIDNMXHUWKUWLYMUWKUGNXJYMUWLUGZDXJQUWEUWGXKXLNCMXHUWLUVKUWMCX
      JYMUVKUGNXJQUWGUVDXKXLXMCMXHUVKXNXGUWCXOYMGUGCXJXPXQUVDESZUVFUVQUVHUVSUWN
      UVEUVPFUVDEAVOXRUWNUVGUVRGUVDEBVOXRXSXTUQVKYAUVOYKOQRZWRZUWOFGRZYKYBZUWPU
      UOUWRUUROQFGUUSUUTJKYCUQUWOUWQYKYDUQYQUWOYKUUKUUIYQUWOUUMUULQUUHSUWOUUISU
      LQUUHOYEUQYFYGYHMYQEYKYIYJYH $.
      $( [9-Sep-2011] $)
  $}

  ${
    $d m F $.  $d m S $.  $d m T $.  $d m U $.  $d f m X $.  $d f m Y $.
    $d f m Z $.  $d f h m $.  $d g h m $.
    stb3xpl.s $e |- S = ( g e. _V |-> ( g ` 1 ) ) $.
    stb3xpl.t $e |- T = ( g e. _V |-> ( g ` 2 ) ) $.
    stb3xpl.u $e |- U = ( g e. _V |-> ( g ` 3 ) ) $.
    stb3xpl.3 $e |- X e. _V $.
    stb3xpl.4 $e |- Y e. _V $.
    stb3xpl.z $e |- Z e. _V $.
    stb3xpl.5 $e |- F = StrBldr ( 3 , f ,
                     ( ( S ` f ) = X /\ ( T ` f ) = Y /\ ( U ` f ) = Z ) ) $.
    $( The explicit equivalent of a constructed 3-member structure.  This
       theorem lets us go back and forth between constructed and explicit
       representations. $)
    stb3xpl $p |- F = { <. 1 , X >. , <. 2 , Y >. , <. 3 , Z >. } $=
      ( vm vh c1 cop c2 c3 ctp wceq cv cfv cfz co wral cif w3a wcel w3o caddc
      visset eltp df-3 2cn ax1cn addcomi eqtri opreq2i cz 1z fztp ax-mp eleq2i
      biid df-2 eqeq2i 3orbi123i 3bitr4i wne clt wbr 1lt2 1re 2re ltnei necom
      mpbi 1lt3 3re 2lt3 cr elisseti fvtp1 mp3an fveq2 iftrue 3eqtr4a fvtp2 wn
      pm13.181 mpan2 df-ne sylib iffalse syl eqtrd fvtp3 3jaoi sylbi eqeq2d
      ralbiia fztpval bitri strcval eqeq1i 3anbi123i stb3val1 stb3val2
      stb3val3 mpbir3an wfn wb cstru cdif wa stb3el cn 3nn ccstr hbstb1 hbxfr
      cvv cmpt ax-17 fveq1 cbvmpt 3eqtri hbmpt1 hbfv hbeq hb3an eqeq1d
      3anbi123d elstrdiff simpli fntp tpeq3 tpeq2 3eqtr4i fneq2i mpbir eqfnfv2
      mp2an ) FSGTUAHTUBITUCZUDZQUEZFUFZUUJUUHUFZUDZQSUBUGUHZUIZUUOSFUFGUDZUAFU
      FHUDZUBFUFIUDZUUOUUKUUJSUDZGUUJUAUDZHIUJZUJZUDZQUUNUIUUPUUQUURUKUUMUVCQUU
      NUUJUUNULZUULUVBUUKUVDUUSUUTUUJUBUDZUMZUULUVBUDZUUJSSSUNUHZSUAUNUHZUCZULU
      USUUJUVHUDZUUJUVIUDZUMUVDUVFUUJSUVHUVIQUOUPUUNUVJUUJUUNSUVIUGUHZUVJUBUVIS
      UGUBUASUNUHUVIUQUASURUSUTVAZVBZSVCULUVMUVJUDVDSVEVFZVAVGUUSUUSUUTUVKUVEUV
      LUUSVHUAUVHUUJVIVJUBUVIUUJUVNVJVKVLUUSUVGUUTUVEUUSSUUHUFZGUULUVBSUAVMZSUB
      VMZUAUBVMZUVQGUDUASVMZUVRSUAVNVOUWAVPSUAVQVRVSVFZUASVTWAZUBSVMZUVSSUBVNVO
      UWDWBSUBVQWCVSVFZUBSVTWAZUBUAVMZUVTUAUBVNVOUWGWDUAUBVRWCVSVFZUBUAVTWAZSUA
      UBGHISWEVQWFZUAWEVRWFZUBWEWCWFZMNOWGWHUUJSUUHWIUUSGUVAWJWKUUTUAUUHUFZHUUL
      UVBUVRUVSUVTUWMHUDUWCUWFUWISUAUBGHIUWJUWKUWLMNOWLWHUUJUAUUHWIUUTUVBUVAHUU
      TUUSWMZUVBUVAUDZUUTUUJSVMZUWNUUTUWAUWPUWBUUJUASWNWOUUJSWPZWQUUSGUVAWRZWSU
      UTHIWJWTWKUVEUBUUHUFZIUULUVBUVRUVSUVTUWSIUDUWCUWFUWISUAUBGHIUWJUWKUWLMNOX
      AWHUUJUBUUHWIUVEUVBUVAIUVEUWNUWOUVEUWPUWNUVEUWDUWPUWEUUJUBSWNWOUWQWQUWRWS
      UVEUUTWMZUVAIUDUVEUUJUAVMZUWTUVEUWGUXAUWHUUJUBUAWNWOUUJUAWPWQUUTHIWRWSWTW
      KXBXCXDXEQGHIFXFXGDUEZAUFZGUDZUXBBUFZHUDZUXBCUFZIUDZUKZGHIDFMNOUXDSUXBUFZ
      GUDUXFUAUXBUFZHUDUXHUBUXBUFZIUDUXCUXJGAEUXBSDUOZJXHXIUXEUXKHBEUXBUAUXMKXH
      XIUXGUXLICEUXBUBUXMLXHXIXJZPXKUXIGHIDFMNOUXNPXLUXIGHIDFMNOUXNPXMXNFUUNXOZ
      UUHUUNXOZUUIUUOXPUXOFAUFZGUDZFBUFZHUDZFCUFZIUDZUKZFUXIDUBXQUXIDUBSUNUHXQX
      RULZUXOUYCXSZUXIGHIDFMNOUXNPXTUBYAULUYDUYEXPYBUXIUYCQDFUBDQFUXIDUBYCPUXIQ
      DUBYDYEZUXRUXTUYBDDQQUXQGDQFADQADYFUXJYGZAEYFSEUEZUFZYGRYFSRUEZUFZYGUYGJE
      RQYFUYIUYKUUJUYIULRYHUUJUYKULZEYHSUYHUYJYIYJRDQYFUYKUXJUYLDYHUUJUXJULRYHS
      UYJUXBYIYJYKDQYFUXJYLYEUYFYMUUJGULDYHYNDQQUXSHDQFBDQBDYFUXKYGZBEYFUAUYHUF
      ZYGRYFUAUYJUFZYGUYMKERQYFUYNUYOUUJUYNULRYHUUJUYOULZEYHUAUYHUYJYIYJRDQYFUY
      OUXKUYPDYHUUJUXKULRYHUAUYJUXBYIYJYKDQYFUXKYLYEUYFYMUUJHULDYHYNDQQUYAIDQFC
      DQCDYFUXLYGZCEYFUBUYHUFZYGRYFUBUYJUFZYGUYQLERQYFUYRUYSUUJUYRULRYHUUJUYSUL
      ZEYHUBUYHUYJYIYJRDQYFUYSUXLUYTDYHUUJUXLULRYHUBUYJUXBYIYJYKDQYFUXLYLYEUYFY
      MUUJIULDYHYNYOUXBFUDZUXDUXRUXFUXTUXHUYBVUAUXCUXQGUXBFAWIYPVUAUXEUXSHUXBFB
      WIYPVUAUXGUYAIUXBFCWIYPYQYRVFWAYSUXPUUHSUAUBUCZXOZUVRUVSUVTVUCUWCUWFUWISU
      AUBGHIUWJUWKUWLMNOYTWHUUNVUBUUHUVMUVJUUNVUBUVPUVOVUBSUAUVIUCZUVJUBUVIUDVU
      BVUDUDUVNUBUVISUAUUAVFUAUVHUDVUDUVJUDVIUAUVHSUVIUUBVFVAUUCUUDUUEQUUNFUUHU
      UFUUGUUE $.
      $( [16-Sep-2011] $)
  $}

  ${
    $d f X $.  $d f Y $.
    stb2str.s $e |- S = ( g e. _V |-> ( g ` 1 ) ) $.
    stb2str.t $e |- T = ( g e. _V |-> ( g ` 2 ) ) $.
    stb2str.3 $e |- X e. _V $.
    stb2str.4 $e |- Y e. _V $.
    stb2str.5 $e |- F
                 = StrBldr ( 2 , f , ( ( S ` f ) = X /\ ( T ` f ) = Y ) ) $.
    $( Structure scaffold property of a 2-member constructed structure. $)
    stb2str $p |- F e. Struct ( 2 , f , T. ) $=
      ( cv cfv wceq wa c1 c2 visset strcval eqeq1i anbi12i stb2cl ) CMZANZFOZUD
      BNZGOZPFGCEJKUFQUDNZFOUHRUDNZGOUEUIFADUDQCSZHTUAUGUJGBDUDRUKITUAUBLUC $.
      $( [9-Sep-2011] $)

    $( The first component of a 2-member constructed structure. $)
    stb2v1 $p |- X = ( S ` F ) $=
      ( cfv c1 cv wceq wa c2 ccstr cvv stbex eqeltri strcval visset eqeq1i
      anbi12i stb2val1 eqtr2i ) EAMNEMFADENECOZAMZFPZUIBMZGPZQZCRSTLUNCRUAUBHUC
      UNFGCEJKUKNUIMZFPUMRUIMZGPUJUOFADUINCUDZHUCUEULUPGBDUIRUQIUCUEUFLUGUH $.
      $( [9-Sep-2011] $)

    $( The second component of a 2-member constructed structure. $)
    stb2v2 $p |- Y = ( T ` F ) $=
      ( cfv c2 cv wceq wa ccstr cvv stbex eqeltri strcval c1 visset eqeq1i
      anbi12i stb2val2 eqtr2i ) EBMNEMGBDENECOZAMZFPZUIBMZGPZQZCNRSLUNCNTUAIUBU
      NFGCEJKUKUCUIMZFPUMNUIMZGPUJUOFADUIUCCUDZHUBUEULUPGBDUINUQIUBUEUFLUGUH $.
      $( [9-Sep-2011] $)
  $}

  ${
    $d g X $.  $d g Y $.  $d f g m $.
    stb2strx.3 $e |- X e. _V $.
    stb2strx.4 $e |- Y e. _V $.
    stb2strx.5 $e |- F = { <. 1 , X >. , <. 2 , Y >. } $.
    $( Structure scaffold property of a 2-member explicitly given structure. $)
    stb2strx $p |- F e. Struct ( 2 , f , T. ) $=
      ( vg vm wtru c2 cstru cvv c1 cv cfv cmpt eqid cop cpr wceq wa ccstr
      stb2xpl eqtr4i stb2str cle wbr cfz co wfn cn wrex cab fneq1 anbi2d
      rexbidv anbi1d cbvabv df-struct 3eqtr4ri eleqtri ) BJHKLZJAKLZHMNHOZPQZHM
      KVEPQZHHBCDVFRZVGRZEFBNCSKDSTVEVFPCUAVEVGPDUAUBHKUCZGVFVGHHVJCDVHVIEFVJRU
      DUEUFKIOZUGUHZAOZNVKUIUJZUKZUBZIULUMZJUBZAUNVLVEVNUKZUBZIULUMZJUBZHUNVDVC
      VRWBAHVMVEUAZVQWAJWCVPVTIULWCVOVSVLVNVMVEUOUPUQURUSJAIKUTJHIKUTVAVB $.
      $( [9-Sep-2011] $)

    ${
      stb2v1x.s $e |- S = ( f e. _V |-> ( f ` 1 ) ) $.
      $( The first component of a 2-member explicitly given structure. $)
      stb2v1x $p |- X = ( S ` F ) $=
        ( vg cvv c2 cv cfv cmpt eqid c1 cop cpr wceq wa ccstr stb2xpl eqtr4i
        stb2v1 ) ABKLBMNOZJBCDEIUFPZFGCQDRLERSJMZANDTUHUFNETUAJLUBZHAUFJBUIDEIU
        GFGUIPUCUDUE $.
        $( [9-Sep-2011] $)
    $}

    ${
      stb2v2x.t $e |- T = ( f e. _V |-> ( f ` 2 ) ) $.
      $( The second component of a 2-member explicitly given structure. $)
      stb2v2x $p |- Y = ( T ` F ) $=
        ( vg cvv c1 cv cfv cmpt eqid cop c2 cpr wceq wa ccstr stb2xpl eqtr4i
        stb2v2 ) BKLBMNOZAJBCDEUFPZIFGCLDQREQSJMZUFNDTUHANETUAJRUBZHUFAJBUIDEUG
        IFGUIPUCUDUE $.
        $( [9-Sep-2011] $)
    $}
  $}

  ${
    $d f X $.  $d f Y $.  $d f Z $.
    stb3str.s $e |- S = ( g e. _V |-> ( g ` 1 ) ) $.
    stb3str.t $e |- T = ( g e. _V |-> ( g ` 2 ) ) $.
    stb3str.u $e |- U = ( g e. _V |-> ( g ` 3 ) ) $.
    stb3str.3 $e |- X e. _V $.
    stb3str.4 $e |- Y e. _V $.
    stb3str.z $e |- Z e. _V $.
    stb3str.5 $e |- F = StrBldr ( 3 , f ,
                       ( ( S ` f ) = X /\ ( T ` f ) = Y /\ ( U ` f ) = Z ) ) $.
    $( Structure scaffold property of a 3-member constructed structure. $)
    stb3str $p |- F e. Struct ( 3 , f , T. ) $=
      ( cv cfv wceq w3a c1 c2 c3 visset strcval eqeq1i 3anbi123i stb3cl ) DQZAR
      ZGSZUIBRZHSZUICRZISZTGHIDFMNOUKUAUIRZGSUMUBUIRZHSUOUCUIRZISUJUPGAEUIUADUD
      ZJUEUFULUQHBEUIUBUSKUEUFUNURICEUIUCUSLUEUFUGPUH $.
      $( [16-Sep-2011] $)

    $( The first component of a 3-member constructed structure. $)
    stb3v1 $p |- X = ( S ` F ) $=
      ( cfv c1 cv wceq w3a c3 ccstr cvv stbex eqeltri strcval c2 visset eqeq1i
      3anbi123i stb3val1 eqtr2i ) FAQRFQGAEFRFDSZAQZGTZUNBQZHTZUNCQZITZUAZDUBUC
      UDPVADUBUEUFJUGVAGHIDFMNOUPRUNQZGTURUHUNQZHTUTUBUNQZITUOVBGAEUNRDUIZJUGUJ
      UQVCHBEUNUHVEKUGUJUSVDICEUNUBVELUGUJUKPULUM $.
      $( [16-Sep-2011] $)

    $( The second component of a 3-member constructed structure. $)
    stb3v2 $p |- Y = ( T ` F ) $=
      ( cfv c2 cv wceq w3a c3 ccstr cvv stbex eqeltri strcval c1 visset eqeq1i
      3anbi123i stb3val2 eqtr2i ) FBQRFQHBEFRFDSZAQZGTZUNBQZHTZUNCQZITZUAZDUBUC
      UDPVADUBUEUFKUGVAGHIDFMNOUPUHUNQZGTURRUNQZHTUTUBUNQZITUOVBGAEUNUHDUIZJUGU
      JUQVCHBEUNRVEKUGUJUSVDICEUNUBVELUGUJUKPULUM $.
      $( [16-Sep-2011] $)

    $( The second component of a 3-member constructed structure. $)
    stb3v3 $p |- Z = ( U ` F ) $=
      ( cfv c3 cv wceq w3a ccstr cvv stbex eqeltri strcval c1 c2 visset eqeq1i
      3anbi123i stb3val3 eqtr2i ) FCQRFQICEFRFDSZAQZGTZUNBQZHTZUNCQZITZUAZDRUBU
      CPVADRUDUELUFVAGHIDFMNOUPUGUNQZGTURUHUNQZHTUTRUNQZITUOVBGAEUNUGDUIZJUFUJU
      QVCHBEUNUHVEKUFUJUSVDICEUNRVELUFUJUKPULUM $.
      $( [16-Sep-2011] $)
  $}

  ${
    $d g X $.  $d g Y $.  $d g Z $.  $d f g m $.
    stb3strx.3 $e |- X e. _V $.
    stb3strx.4 $e |- Y e. _V $.
    stb3strx.z $e |- Z e. _V $.
    stb3strx.5 $e |- F = { <. 1 , X >. , <. 2 , Y >. , <. 3 , Z >. } $.
    $( Structure scaffold property of a 3-member explicitly given structure. $)
    stb3strx $p |- F e. Struct ( 3 , f , T. ) $=
      ( vg vm wtru c3 cstru cvv c1 cv cfv cmpt c2 eqid cop ctp wceq w3a ccstr
      stb3xpl eqtr4i stb3str cle wbr cfz co wfn wa cn wrex cab fneq1 anbi2d
      rexbidv anbi1d cbvabv df-struct 3eqtr4ri eleqtri ) BLJMNZLAMNZJOPJQZRSZJO
      TVIRSZJOMVIRSZJJBCDEVJUAZVKUAZVLUAZFGHBPCUBTDUBMEUBUCVIVJRCUDVIVKRDUDVIVL
      REUDUEJMUFZIVJVKVLJJVPCDEVMVNVOFGHVPUAUGUHUIMKQZUJUKZAQZPVQULUMZUNZUOZKUP
      UQZLUOZAURVRVIVTUNZUOZKUPUQZLUOZJURVHVGWDWHAJVSVIUDZWCWGLWIWBWFKUPWIWAWEV
      RVTVSVIUSUTVAVBVCLAKMVDLJKMVDVEVF $.
      $( [16-Sep-2011] $)

    ${
      stb3v1x.s $e |- S = ( f e. _V |-> ( f ` 1 ) ) $.
      $( The first component of a 3-member explicitly given structure. $)
      stb3v1x $p |- X = ( S ` F ) $=
        ( vg cvv c2 cv cfv cmpt c3 eqid c1 cop ctp wceq w3a ccstr stb3xpl
        eqtr4i stb3v1 ) ABMNBOZPQZBMRUIPQZLBCDEFKUJSZUKSZGHICTDUANEUARFUAUBLOZA
        PDUCUNUJPEUCUNUKPFUCUDLRUEZJAUJUKLBUODEFKULUMGHIUOSUFUGUH $.
        $( [16-Sep-2011] $)
    $}

    ${
      stb3v2x.t $e |- T = ( f e. _V |-> ( f ` 2 ) ) $.
      $( The second component of a 3-member explicitly given structure. $)
      stb3v2x $p |- Y = ( T ` F ) $=
        ( vg cvv c1 cv cfv cmpt c3 eqid cop c2 ctp wceq w3a ccstr stb3xpl
        eqtr4i stb3v2 ) BMNBOZPQZABMRUIPQZLBCDEFUJSZKUKSZGHICNDTUAETRFTUBLOZUJP
        DUCUNAPEUCUNUKPFUCUDLRUEZJUJAUKLBUODEFULKUMGHIUOSUFUGUH $.
        $( [16-Sep-2011] $)
    $}

    ${
      stb3v3x.u $e |- U = ( f e. _V |-> ( f ` 3 ) ) $.
      $( The second component of a 3-member explicitly given structure. $)
      stb3v3x $p |- Z = ( U ` F ) $=
        ( vg cvv c1 cv cfv cmpt c2 eqid cop c3 ctp wceq w3a ccstr stb3xpl
        eqtr4i stb3v3 ) BMNBOZPQZBMRUIPQZALBCDEFUJSZUKSZKGHICNDTRETUAFTUBLOZUJP
        DUCUNUKPEUCUNAPFUCUDLUAUEZJUJUKALBUODEFULUMKGHIUOSUFUGUH $.
        $( [16-Sep-2011] $)
    $}
  $}

