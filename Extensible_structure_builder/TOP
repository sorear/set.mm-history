$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                    Extensible structure builder
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)


  ${
    $d g C $.  $d g F $.  $d f g N $.
    strcval.f $e |- F e. _V $.
    strcval.c $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    $( Value of a (defined) structure component.  Normally, ` C ` is a defined
       constant symbol such as ` Base ` and ` N ` is a fixed integer such as
       ` 1 ` . $)
    strcval $p |- ( C ` F ) = ( F ` N ) $=
      ( vg cv cfv wceq fveq2 fveq1 eqeq12d cvv wcel visset fvex fvmpt ax-mp
      vtocl ) GHZAIZDUAIZJZCAIZDCIZJGCEUACJUBUEUCUFUACAKDUACLMUANOUDGPBUADBHZIU
      CNADUGUALFDUAQRST $.
      $( [9-Sep-2011] $)
  $}

  ${
    str2vx.x $e |- X e. _V $.
    str2vx.y $e |- Y e. _V $.
    str2vx.m $e |- M e. _V $.
    str2vx.n $e |- N e. _V $.
    str2vx.e $e |- M =/= N $.
    str2vx.f $e |- F = { <. M , X >. , <. N , Y >. } $.
    ${
      $d f M $.
      str2v1x.s $e |- S = ( f e. _V |-> ( f ` M ) ) $.
      $( The first component of a 2-member explicitly given structure.  See
         ~ str2v1 for a version independent of the component index. $)
      str2v1x $p |- X = ( S ` F ) $=
        ( cfv cop cpr fveq2i prex strcval wne wceq fvpr1 ax-mp 3eqtrri ) CAODFP
        ZEGPZQZAODUHOZFCUHAMRABUHDUFUGSNTDEUAUIFUBLDEFGJKHIUCUDUE $.
        $( [19-Oct-2012] $)
    $}

    ${
      $d f N $.
      str2v2x.t $e |- T = ( f e. _V |-> ( f ` N ) ) $.
      $( The second component of a 2-member explicitly given structure.  See
         ~ str2v2 for a version independent of the component index. $)
      str2v2x $p |- Y = ( T ` F ) $=
        ( cfv cop cpr fveq2i prex strcval wne wceq fvpr2 ax-mp 3eqtrri ) CAODFP
        ZEGPZQZAOEUHOZGCUHAMRABUHEUFUGSNTDEUAUIGUBLDEFGJKHIUCUDUE $.
        $( [19-Oct-2012] $)
    $}
  $}

  ${
    str3vx.x $e |- X e. _V $.
    str3vx.y $e |- Y e. _V $.
    str3vx.z $e |- Z e. _V $.
    str3vx.l $e |- L e. _V $.
    str3vx.m $e |- M e. _V $.
    str3vx.n $e |- N e. _V $.
    str3vx.e $e |- L =/= M $.
    str3vx.e2 $e |- ( L =/= N /\ M =/= N ) $.
    str3vx.f $e |- F = { <. L , X >. , <. M , Y >. , <. N , Z >. } $.
    ${
      $d f L $.
      str3v1x.s $e |- S = ( f e. _V |-> ( f ` L ) ) $.
      $( The first component of a 3-member explicitly given structure.  See
         ~ str3v1 for a version independent of the component index. $)
      str3v1x $p |- X = ( S ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp1 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATDUPTZGCUPARUCABUPDUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQGUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}

    ${
      $d f M $.
      str3v2x.t $e |- T = ( f e. _V |-> ( f ` M ) ) $.
      $( The second component of a 3-member explicitly given structure.  See
         ~ str3v2 for a version independent of the component index. $)
      str3v2x $p |- Y = ( T ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp2 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATEUPTZHCUPARUCABUPEUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQHUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}

    ${
      $d f N $.
      str3v3x.u $e |- U = ( f e. _V |-> ( f ` N ) ) $.
      $( The third component of a 3-member explicitly given structure.  See
         ~ str3v2 for a version independent of the component index. $)
      str3v3x $p |- Z = ( U ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp3 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATFUPTZICUPARUCABUPFUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQIUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}
  $}

  $c ndx $.

  $( Extend class notation with index extraction function. $)
  cnx $a class ndx $.

  ${
    $( Define an index extraction function on NN. $)
    df-ndx $a |- ndx = ( _I |` NN ) $.

  $}

  ${
    $d f N $.
    ndxarg.1 $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    $( Extraction of the argument from a defined structure component. $)
    ndxarg $p |- ( N e. NN -> ( C ` ndx ) = N ) $=
      ( cn wcel cnx cfv cid cres cvv df-ndx nnex resiexg ax-mp eqeltri strcval
      fveq1i fvresi syl5eq ) CEFZGAHCGHZCABGCGIEJZKLEKFUCKFMEKNOPDQUAUBCUCHCCGU
      CLRECSTT $.
      $( [18-Oct-2012] $)
  $}

  ${
    $d f N $.
    ndxid.1 $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    ndxid.2 $e |- N e. NN $.
    $( A structure component is defined by its own index.  This theorem is
       useful for avoiding reference to the actual argument index in proofs. $)
    ndxid $p |- C = ( f e. _V |-> ( f ` ( C ` ndx ) ) ) $=
      ( cvv cv cfv cmpt cnx eqid cn wcel wceq ndxarg ax-mp fveq2i mpteqi
      eqtr4i ) ABFCBGZHZIBFJAHZTHZIDBFUCFUAFKUBCTCLMUBCNEABCDOPQRS $.
      $( [19-Oct-2012] $)
  $}

  ${
    $d f S $.  $d f T $.
    str2v.s $e |- S = ( f e. _V |-> ( f ` ( S ` ndx ) ) ) $.
    str2v.t $e |- T = ( f e. _V |-> ( f ` ( T ` ndx ) ) ) $.
    str2v.f $e |- F = { <. ( S ` ndx ) , X >. , <. ( T ` ndx ) , Y >. } $.
    str2v.x $e |- X e. _V $.
    str2v.y $e |- Y e. _V $.
    str2v.n $e |- ( S ` ndx ) =/= ( T ` ndx ) $.
    $( The first component of a 2-member constructed structure.  Unlike
       ~ str2v1x , this version is independent of the actual indices. $)
    str2v1 $p |- X = ( S ` F ) $=
      ( cnx cfv fvex str2v1x ) ACDMANMBNEFJKMAOMBOLIGP $.
      $( [20-Oct-2012] $)

    $( The second component of a 2-member constructed structure.  Unlike
       ~ str2v2x , this version is independent of the actual indices. $)
    str2v2 $p |- Y = ( T ` F ) $=
      ( cnx cfv fvex str2v2x ) BCDMANMBNEFJKMAOMBOLIHP $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d f S $.  $d f T $.  $d f U $.
    str3v.s $e |- S = ( f e. _V |-> ( f ` ( S ` ndx ) ) ) $.
    str3v.t $e |- T = ( f e. _V |-> ( f ` ( T ` ndx ) ) ) $.
    str3v.u $e |- U = ( f e. _V |-> ( f ` ( U ` ndx ) ) ) $.
    str3v.f $e |- F = { <. ( S ` ndx ) , X >. , <. ( T ` ndx ) , Y >. ,
         <. ( U ` ndx ) , Z >. } $.
    str3v.x $e |- X e. _V $.
    str3v.y $e |- Y e. _V $.
    str3v.z $e |- Z e. _V $.
    str3v.n $e |- ( S ` ndx ) =/= ( T ` ndx ) $.
    str3v.n2 $e |- ( ( S ` ndx ) =/= ( U ` ndx )
                       /\ ( T ` ndx ) =/= ( U ` ndx ) ) $.
    $( The first component of a 3-member constructed structure.  Unlike
       ~ str3v1x , this version is independent of the actual indices. $)
    str3v1 $p |- X = ( S ` F ) $=
      ( cnx cfv fvex str3v1x ) ADERASRBSRCSFGHMNORATRBTRCTPQLIUA $.
      $( [8-Jun-2013] $)

    $( The second component of a 3-member constructed structure.  Unlike
       ~ str3v2x , this version is independent of the actual indices. $)
    str3v2 $p |- Y = ( T ` F ) $=
      ( cnx cfv fvex str3v2x ) BDERASRBSRCSFGHMNORATRBTRCTPQLJUA $.
      $( [8-Jun-2013] $)

    $( The third component of a 3-member constructed structure.  Unlike
       ~ str3v3x , this version is independent of the actual indices. $)
    str3v3 $p |- Z = ( U ` F ) $=
      ( cnx cfv fvex str3v3x ) CDERASRBSRCSFGHMNORATRBTRCTPQLKUA $.
      $( [8-Jun-2013] $)
  $}

