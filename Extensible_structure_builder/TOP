$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                    Extensible structure builder
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)


  ${
    $d g C $.  $d g F $.  $d f g N $.
    strcval.f $e |- F e. _V $.
    strcval.c $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    $( Value of a (defined) structure component.  Normally, ` C ` is a defined
       constant symbol such as ` Base ` and ` N ` is a fixed integer such as
       ` 1 ` . $)
    strcval $p |- ( C ` F ) = ( F ` N ) $=
      ( vg cv cfv wceq fveq2 fveq1 eqeq12d cvv wcel visset fvex fvmpt ax-mp
      vtocl ) GHZAIZDUAIZJZCAIZDCIZJGCEUACJUBUEUCUFUACAKDUACLMUANOUDGPBUADBHZIU
      CNADUGUALFDUAQRST $.
      $( [9-Sep-2011] $)
  $}

  ${
    $d f N $.
    strfvx.s $e |- S e. _V $.
    strfvx.f $e |- Fun S $.
    strfvx.e $e |- E = ( f e. _V |-> ( f ` N ) ) $.
    strfvx.n $e |- <. N , C >. e. S $.
    $( Extract a structure component ` C ` (such as the base set) with a
       component extractor ` E ` (such as the base set extractor ~ df-base )
       with numeric index ` N ` , from an explicit structure ` S ` .  See
       ~ strfv for a version independent of the component index ` N ` . $)
    strfvx $p |- ( C e. V -> C = ( E ` S ) ) $=
      ( wcel cfv strcval cop wceq wfun wi funopfvg mpan2 mpi syl5req ) AFKZBDLE
      BLZADCBEGIMUBEANBKZUCAOZJUBBPUDUEQHEAFBRSTUA $.
      $( [27-Sep-2013] $)
  $}

  ${
    $d f N $.
    strfvxOLD.c $e |- C e. _V $.
    strfvxOLD.s $e |- S e. _V $.
    strfvxOLD.f $e |- Fun S $.
    strfvxOLD.e $e |- E = ( f e. _V |-> ( f ` N ) ) $.
    strfvxOLD.n $e |- <. N , C >. e. S $.
    $( Extract a structure component ` C ` (such as the base set) with a
       component extractor ` E ` (such as the base set extractor ~ df-base )
       with numeric index ` N ` , from an explicit structure ` S ` .  See
       ~ strfv for a version independent of the component index ` N ` . $)
    strfvxOLD $p |- C = ( E ` S ) $=
      ( cfv strcval wfun cop wcel wceq funopfv mp2 eqtr2i ) BDKEBKZADCBEGILBMEA
      NBOTAPHJEABFQRS $.
      $( [27-Sep-1013] $)
  $}

  ${
    str2vxOLD.x $e |- X e. _V $.
    str2vxOLD.y $e |- Y e. _V $.
    str2vxOLD.m $e |- M e. _V $.
    str2vxOLD.n $e |- N e. _V $.
    str2vxOLD.e $e |- M =/= N $.
    str2vxOLD.f $e |- F = { <. M , X >. , <. N , Y >. } $.
    ${
      $d f M $.
      str2v1xOLD.s $e |- S = ( f e. _V |-> ( f ` M ) ) $.
      $( The first component of a 2-member explicitly given structure.  See
         ~ str2v1OLD for a version independent of the component index. $)
      str2v1xOLD $p |- X = ( S ` F ) $=
        ( cfv cop cpr fveq2i prex strcval wne wceq fvpr1OLD ax-mp 3eqtrri ) CAO
        DFPZEGPZQZAODUHOZFCUHAMRABUHDUFUGSNTDEUAUIFUBLDEFGJKHIUCUDUE $.
        $( [19-Oct-2012] $)
    $}

    ${
      $d f N $.
      str2v2xOLD.t $e |- T = ( f e. _V |-> ( f ` N ) ) $.
      $( The second component of a 2-member explicitly given structure.  See
         ~ str2v2OLD for a version independent of the component index. $)
      str2v2xOLD $p |- Y = ( T ` F ) $=
        ( cfv cop cpr fveq2i prex strcval wne wceq fvpr2OLD ax-mp 3eqtrri ) CAO
        DFPZEGPZQZAOEUHOZGCUHAMRABUHEUFUGSNTDEUAUIGUBLDEFGJKHIUCUDUE $.
        $( [19-Oct-2012] $)
    $}
  $}

  ${
    str3vxOLD.x $e |- X e. _V $.
    str3vxOLD.y $e |- Y e. _V $.
    str3vxOLD.z $e |- Z e. _V $.
    str3vxOLD.l $e |- L e. _V $.
    str3vxOLD.m $e |- M e. _V $.
    str3vxOLD.n $e |- N e. _V $.
    str3vxOLD.e $e |- L =/= M $.
    str3vxOLD.e2 $e |- ( L =/= N /\ M =/= N ) $.
    str3vxOLD.f $e |- F = { <. L , X >. , <. M , Y >. , <. N , Z >. } $.
    ${
      $d f L $.
      str3v1xOLD.s $e |- S = ( f e. _V |-> ( f ` L ) ) $.
      $( The first component of a 3-member explicitly given structure.  See
         ~ str3v1OLD for a version independent of the component index. $)
      str3v1xOLD $p |- X = ( S ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp1OLD 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATDUPTZGCUPARUCABUPDUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQGUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}

    ${
      $d f M $.
      str3v2xOLD.t $e |- T = ( f e. _V |-> ( f ` M ) ) $.
      $( The second component of a 3-member explicitly given structure.  See
         ~ str3v2OLD for a version independent of the component index. $)
      str3v2xOLD $p |- Y = ( T ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp2OLD 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATEUPTZHCUPARUCABUPEUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQHUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}

    ${
      $d f N $.
      str3v3xOLD.u $e |- U = ( f e. _V |-> ( f ` N ) ) $.
      $( The third component of a 3-member explicitly given structure.  See
         ~ str3v2OLD for a version independent of the component index. $)
      str3v3xOLD $p |- Z = ( U ` F ) $=
        ( cfv cop ctp fveq2i tpex strcval wne wa wceq fvtp3OLD 3expb mp2an
        3eqtrri ) CATDGUAZEHUAZFIUAZUBZATFUPTZICUPARUCABUPFUMUNUOUDSUEDEUFZDFUF
        ZEFUFZUGUQIUHZPQURUSUTVADEFGHIMNOJKLUIUJUKUL $.
        $( [8-Jun-2013] $)
    $}
  $}

  $c ndx $.

  $( Extend class notation with index extraction function. $)
  cnx $a class ndx $.

  ${
    $( Define an index extraction function on NN. $)
    df-ndx $a |- ndx = ( _I |` NN ) $.

  $}

  ${
    $d f N $.
    ndxarg.1 $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    $( Extraction of the argument from a defined structure component. $)
    ndxarg $p |- ( N e. NN -> ( C ` ndx ) = N ) $=
      ( cn wcel cnx cfv cid cres cvv df-ndx nnex resiexg ax-mp eqeltri strcval
      fveq1i fvresi syl5eq ) CEFZGAHCGHZCABGCGIEJZKLEKFUCKFMEKNOPDQUAUBCUCHCCGU
      CLRECSTT $.
      $( [18-Oct-2012] $)

    ${
      ndxargi.2 $e |- N e. NN $.
      $( Extraction of the argument from a defined structure component. $)
      ndxargi $p |- ( C ` ndx ) = N $=
        ( cn wcel cnx cfv wceq ndxarg ax-mp ) CFGHAICJEABCDKL $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    $d f N $.
    ndxid.1 $e |- C = ( f e. _V |-> ( f ` N ) ) $.
    ndxid.2 $e |- N e. NN $.
    $( A structure component is defined by its own index.  This theorem is
       useful for avoiding reference to the actual argument index in proofs. $)
    ndxid $p |- C = ( f e. _V |-> ( f ` ( C ` ndx ) ) ) $=
      ( cvv cv cfv cmpt cnx eqid ndxargi fveq2i mpteqi eqtr4i ) ABFCBGZHZIBFJAH
      ZPHZIDBFSFQFKRCPABCDELMNO $.
      $( [19-Oct-2012] $)
  $}

  ${
    $d f E $.
    strfv.s $e |- S e. _V $.
    strfv.f $e |- Fun S $.
    strfv.e $e |- E = ( f e. _V |-> ( f ` ( E ` ndx ) ) ) $.
    strfv.n $e |- <. ( E ` ndx ) , C >. e. S $.
    $( Extract a structure component ` C ` (such as the base set) with a
       component extractor ` E ` (such as the base set extractor ~ df-base )
       with index ` N ` , from an explicit structure ` S ` .  Unlike
       ~ strfvx , this version is independent of the actual index ` N ` . $)
    strfv $p |- ( C e. V -> C = ( E ` S ) ) $=
      ( cnx cfv strfvx ) ABCDJDKEFGHIL $.
      $( [27-Sep-2013] $)
  $}

  ${
    $d f E $.
    strfvOLD.c $e |- C e. _V $.
    strfvOLD.s $e |- S e. _V $.
    strfvOLD.f $e |- Fun S $.
    strfvOLD.e $e |- E = ( f e. _V |-> ( f ` ( E ` ndx ) ) ) $.
    strfvOLD.n $e |- <. ( E ` ndx ) , C >. e. S $.
    $( Extract a structure component ` C ` (such as the base set) with a
       component extractor ` E ` (such as the base set extractor ~ df-base )
       with index ` N ` , from an explicit structure ` S ` .  Unlike
       ~ strfvxOLD , this version is independent of the actual index ` N ` . $)
    strfvOLD $p |- C = ( E ` S ) $=
      ( cnx cfv strfvxOLD ) ABCDJDKEFGHIL $.
      $( [27-Sep-1013] $)
  $}

  ${
    $d f S $.  $d f T $.
    str2vOLD.s $e |- S = ( f e. _V |-> ( f ` ( S ` ndx ) ) ) $.
    str2vOLD.t $e |- T = ( f e. _V |-> ( f ` ( T ` ndx ) ) ) $.
    str2vOLD.f $e |- F = { <. ( S ` ndx ) , X >. , <. ( T ` ndx ) , Y >. } $.
    str2vOLD.x $e |- X e. _V $.
    str2vOLD.y $e |- Y e. _V $.
    str2vOLD.n $e |- ( S ` ndx ) =/= ( T ` ndx ) $.
    $( The first component of a 2-member constructed structure.  Unlike
       ~ str2v1xOLD , this version is independent of the actual indices. $)
    str2v1OLD $p |- X = ( S ` F ) $=
      ( cnx cfv fvex str2v1xOLD ) ACDMANMBNEFJKMAOMBOLIGP $.
      $( [20-Oct-2012] $)

    $( The second component of a 2-member constructed structure.  Unlike
       ~ str2v2xOLD , this version is independent of the actual indices. $)
    str2v2OLD $p |- Y = ( T ` F ) $=
      ( cnx cfv fvex str2v2xOLD ) BCDMANMBNEFJKMAOMBOLIHP $.
      $( [20-Oct-2012] $)
  $}

  ${
    $d f S $.  $d f T $.  $d f U $.
    str3vOLD.s $e |- S = ( f e. _V |-> ( f ` ( S ` ndx ) ) ) $.
    str3vOLD.t $e |- T = ( f e. _V |-> ( f ` ( T ` ndx ) ) ) $.
    str3vOLD.u $e |- U = ( f e. _V |-> ( f ` ( U ` ndx ) ) ) $.
    str3vOLD.f $e |- F = { <. ( S ` ndx ) , X >. , <. ( T ` ndx ) , Y >. ,
         <. ( U ` ndx ) , Z >. } $.
    str3vOLD.x $e |- X e. _V $.
    str3vOLD.y $e |- Y e. _V $.
    str3vOLD.z $e |- Z e. _V $.
    str3vOLD.n $e |- ( S ` ndx ) =/= ( T ` ndx ) $.
    str3vOLD.n2 $e |- ( ( S ` ndx ) =/= ( U ` ndx )
                       /\ ( T ` ndx ) =/= ( U ` ndx ) ) $.
    $( The first component of a 3-member constructed structure.  Unlike
       ~ str3v1xOLD , this version is independent of the actual indices. $)
    str3v1OLD $p |- X = ( S ` F ) $=
      ( cnx cfv fvex str3v1xOLD ) ADERASRBSRCSFGHMNORATRBTRCTPQLIUA $.
      $( [8-Jun-2013] $)

    $( The second component of a 3-member constructed structure.  Unlike
       ~ str3v2xOLD , this version is independent of the actual indices. $)
    str3v2OLD $p |- Y = ( T ` F ) $=
      ( cnx cfv fvex str3v2xOLD ) BDERASRBSRCSFGHMNORATRBTRCTPQLJUA $.
      $( [8-Jun-2013] $)

    $( The third component of a 3-member constructed structure.  Unlike
       ~ str3v3xOLD , this version is independent of the actual indices. $)
    str3v3OLD $p |- Z = ( U ` F ) $=
      ( cnx cfv fvex str3v3xOLD ) CDERASRBSRCSFGHMNORATRBTRCTPQLKUA $.
      $( [8-Jun-2013] $)
  $}


