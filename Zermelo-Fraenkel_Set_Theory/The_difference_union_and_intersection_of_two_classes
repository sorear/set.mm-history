$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols. $)
  $c \ $. $( Backslash (difference) $)
  $c u. $. $( Cup (union) $)
  $c i^i $. $( Cap (intersection) $)

  $( Extend class notation to include class difference (read:
     'A minus B'). $)
  cdif $a class ( A \ B ) $.

  $( Extend class notation to include union of two classes (read:  'A union
     B'). $)
  cun $a class ( A u. B ) $.

  $( Extend class notation to include intersection of two classes (read:  'A
     intersect B'). $)
  cin $a class ( A i^i B ) $.

  ${
    $d x A $.  $d x B $.
    $( Define class difference.  Definition 5.12 of Takeuti and Zaring,
       p. 20.  Several notations are used in the literature; we chose the ` \ `
       convention (used in Suppes) instead of a minus sign to reserve the
       latter for later use in, e.g., arithmetic. $)
    df-dif $a |- ( A \ B ) = { x | ( x e. A /\ -. x e. B ) } $.
  $}

  $( Define union of two classes. $)
  df-un $a |- ( A u. B ) = ( V \ ( ( V \ A ) \ B ) ) $.

  $( Define intersection of two classes. $)
  df-in $a |- ( A i^i B ) = ( A \ ( V \ B ) ) $.

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for class difference. $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn wa cab cdif cleq4 birand biabdv df-dif 3eqtr4g ) ABE
      ZDFZAGZRCGHZIZDJRBGZTIZDJACKBCKQUAUCDQSUBTABRLMNDACODBCOP $.
      $([10-Feb-97]$)

    $( Equality theorem for class difference. $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn wa cab cdif cleq4 binegd biland biabdv df-dif
      3eqtr4g ) ABEZDFZCGZSAGZHZIZDJTSBGZHZIZDJCAKCBKRUCUFDRUBUETRUAUDABSLMNODC
      APDCBPQ $.
      $([10-Feb-97]$)

    $( Expansion of membership in a class difference. $)
    eldif $p |- ( A e. ( B \ C ) <-> ( A e. B /\ -. A e. C ) ) $=
      ( vx cvv wcel cdif wn wa wb cv wceq cleq3 binegd bi2and df-dif
      vtoclabg elisset adantr pm5.21ni pm2.61i ) AEFZABCGZFZABFZACFZHZIZJDKZBFZ
      UICFZHZIUHDAUCEUIALZUJUEULUGUIABMUMUKUFUIACMNODBCPQUDUBUHAUCRUEUBUGABRSTU
      A $.
      $([29-Apr-94]$)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
      ( cdif wcel wn wa eldif biimp pm3.26d ) ABCDEZABEZACEFZKLMGABCHIJ $.
      $([29-Apr-94]$)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
      ( cdif wcel wn wa eldif biimp pm3.27d ) ABCDEZABEZACEFZKLMGABCHIJ $.
      $([3-May-94]$)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
      ( cdif wcel eldifn con2 ) ACBDEABEACBFG $.
      $([27-Jun-94]$)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
      ( wcel cdif wn wa eldif biimpr exp pm2.15d imp ) ABDZABCEDZFACDZMONMOFZN
      NMPGABCHIJKL $.
      $([28-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass relationship for class difference.  Exercise 14 of Takeuti
       and Zaring, p. 22. $)
    difss $p |- ( A \ B ) (_ A $=
      ( vx cdif cv eldifi ssri ) CABDACEABFG $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Alternate definition of union of two classes.  Definition 5.6 of
       Takeuti and Zaring, p. 16. $)
    dfun2 $p |- ( A u. B ) = { x | ( x e. A \/ x e. B ) } $=
      ( cv wcel wo cun cvv cdif wn eldif visset mpbiran df-un cleq4i wa
      biran ioran 3bitr4 bicon2 cleqabri ) ADZBEZUBCEZFZABCGZUBHHBIZCIZIZEZUBUH
      EZJZUBUFEUEUJUBHEZULUBHUHKALZMUFUIUBBCNOUKUEUBUGEZUDJZPUCJZUPPUKUEJUOUQUP
      UOUMUQUBHBKUNMQUBUGCKUCUDRSTSUA $.
      $([26-Dec-96]$)

    $( Alternate definition of intersection of two classes.  Definition 5.6
       of Takeuti and Zaring, p. 16. $)
    dfin2 $p |- ( A i^i B ) = { x | ( x e. A /\ x e. B ) } $=
      ( cv wcel wa cin cvv cdif df-in cleq4i wn eldif visset mpbiran bicon2
      bilan bitr4 bitr cleqabri ) ADZBEZUACEZFZABCGZUAUEEUABHCIZIZEZUDUEUGUABCJ
      KUHUBUAUFEZLZFUDUABUFMUCUJUBUIUCUIUAHEUCLUAHCMANOPQRST $.
      $([26-Dec-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( x e. A <-> ( x e. B \/ x e. C ) ) $.
    $( Inference from membership to union. $)
    uneqri   $p |- A = ( B u. C ) $=
      ( cv wcel wo cab cun cleqabri dfun2 eqtr4 ) BAFZCGNDGHZAICDJOABEKACDLM $.
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Absorption law for union of classes.  Theorem 23 of Suppes, p. 27. $)
    unabs $p |- A = ( A u. A ) $=
      ( vx cv wcel orabs uneqri ) BAAABCADEF $.
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Commutative law for union of classes.  Exercise 6 of Takeuti and Zaring,
       p. 17. $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cv wcel wo cab cun orcom biab dfun2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGABH
      BAHPQCNOIJCABKCBAKL $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cv wcel wo cab cun cleq4 birord biabdv dfun2 3eqtr4g ) ABEZDF
      ZAGZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
  $}

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i   $p |- ( A u. C ) = ( B u. C )  $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEABCGDH $.
      $([30-Aug-93]$)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.

  ${
    uneq2i.1 $e |- A = B $.
    $( Inference adding union to the left in a class equality. $)
    uneq2i   $p |- ( C u. A ) = ( C u. B )  $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEABCGDH $.
      $([30-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in class union.  Theorem 12 of Suppes, p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cvv wcel cun wo wb cv wceq cleq3 bi2ord dfun2 vtoclabg elisset
      jaoi pm5.21ni pm2.61i ) AEFZABCGZFZABFZACFZHZIDJZBFZUFCFZHUEDAUAEUFAKUGUC
      UHUDUFABLUFACLMDBCNOUBTUEAUAPUCTUDABPACPQRS $.
      $([7-Aug-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass relationship for union of classes.  Theorem 25 of Suppes,
       p. 27. $)
    ssun1 $p |- A (_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssri ) CAABDZCEZAFZNMBFZGMLFNOHMABIJK
      $.
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A (_ ( B u. A ) $=
      ( cun ssun1 uncom sseqtr ) AABCBACABDABEF $.
      $([30-Aug-93]$)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A (_ B -> A (_ ( B u. C ) ) $=
      ( wss cun sstr ssun1 mpi ) ABDBBCEZDAIDABIFBCGH $.

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A (_ B -> A (_ ( C u. B ) ) $=
      ( wss cun sstr ssun2 mpi ) ABDBCBEZDAIDABIFBCGH $.
      $([14-Aug-94]$)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
      ( cun ssun1 ssi ) BBCDABCEF $.

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
      ( cun ssun2 ssi ) BCBDABCEF $.
      $([30-Aug-93]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( A relationship between subclass and union.  Theorem 26 of Suppes,
       p. 27. $)
    ssequn1 $p |- ( A (_ B <-> B = ( A u. B ) ) $=
      ( vx wss cv wcel wo cab wceq cun wi wal wb pm4.72 bial df-ss cleqab
      3bitr4 dfun2 cleq2i bitr4 ) ABDZBCEZAFZUCBFZGZCHZIZBABJZIUDUEKZCLUEUFMZCL
      UBUHUJUKCUDUENOCABPUFCBQRUIUGBCABSTUA $.
      $([30-Aug-93]$)
  $}

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A (_ B <-> B = ( B u. A ) ) $=
      ( wss cun wceq ssequn1 uncom cleq2i bitr ) ABCBABDZEBBADZEABFJKBABGHI $.
      $([13-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Union of subclasses. $)
    unss $p |- ( ( A u. B ) (_ C <-> ( A (_ C /\ B (_ C ) ) $=
      ( vx cv cun wcel wi wal wa wss wo elun birim jaob bitr bial df-ss bi2an
      19.26 bitr4 3bitr4 ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZUDCKACKZBCK
      ZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUMUNURUOUSDAC
      RDBCRSUIUKDTUAUB $.
      $([4-Jun-94]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    $( Commutative law for intersection of classes.  Exercise 7 of Takeuti and
       Zaring, p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cv wcel wa cab cin ancom biab dfin2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGABH
      BAHPQCNOIJCABKCBAKL $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( x e. A <-> ( x e. B /\ x e. C ) ) $.
    $( Inference from membership to union. $)
    ineqri   $p |- A = ( B i^i C ) $=
      ( cv wcel wa cab cin cleqabri dfin2 eqtr4 ) BAFZCGNDGHZAICDJOABEKACDLM $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cv wcel wa cab cin cleq4 birand biabdv dfin2 3eqtr4g ) ABEZDF
      ZAGZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
      $([14-Dec-93]$)
  $}

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i   $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEABCGDH $.
      $([26-Dec-93]$)
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d   $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([10-Apr-94]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $([26-Dec-93]$)

  ${
    ineq2i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq2i   $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEABCGDH $.
      $([26-Dec-93]$)
  $}

  ${
    ineq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality inference for intersection of two classes. $)
    ineq2d   $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([10-Apr-94]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq3 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
      ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
      $([8-May-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of Suppes, p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cvv wcel cin wa wb cv wceq cleq3 bi2and dfin2 vtoclabg elisset
      adantl pm5.21ni pm2.61i ) AEFZABCGZFZABFZACFZHZIDJZBFZUFCFZHUEDAUAEUFAKUG
      UCUHUDUFABLUFACLMDBCNOUBTUEAUAPUDTUCACPQRS $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Absorption law for intersection of classes.  Theorem 15 of Suppes,
       p. 26. $)
    inabs $p |- A = ( A i^i A ) $=
      ( vx cv wcel anabs ineqri ) BAAABCADEF $.
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for intersection of classes.  Exercise 9 of Takeuti and
       Zaring, p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa elin biran anass bitr bilan 3bitr4 ineqri ) DABEZCEZA
      BCEZDFZPGZSCGZHZSAGZSBGZUAHZHZSQGUCSRGZHUBUCUDHZUAHUFTUHUASABIJUCUDUAKLSP
      CIUGUEUCSBCIMNO $.
      $([3-May-94]$)
  $}

  $( Intersection distributes over itself. $)
  indist $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
      ( cin inabs ineq1i incom inass 3eqtr3 ineq2i 3eqtr4 eqtr ) ABCDZDZAADZMDZ
      ABDZACDZDZAOMAEFANDABRDZDPSNTAQCDBADZCDNTQUACABGFABCHBACHIJAAMHABRHKL $.
      $([6-May-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( A relationship between subclass and intersection.  Exercise 9 of
       Takeuti and Zaring, p. 18. $)
    sseqin1 $p |- ( A (_ B <-> A = ( A i^i B ) ) $=
      ( vx wss cv wcel wa cab wceq cin wi wal wb pm4.71 bial df-ss cleqab
      3bitr4 dfin2 cleq2i bitr4 ) ABDZACEZAFZUCBFZGZCHZIZAABJZIUDUEKZCLUDUFMZCL
      UBUHUJUKCUDUENOCABPUFCAQRUIUGACABSTUA $.
  $}

  $( A relationship between subclass and intersection. $)
  sseqin2 $p |- ( A (_ B <-> A = ( B i^i A ) ) $=
      ( wss cin wceq sseqin1 incom cleq2i bitr ) ABCAABDZEABADZEABFJKAABGHI $.
      $([17-May-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of Takeuti and Zaring, p. 18. $)
    inss1 $p |- ( A i^i B ) (_ A $=
      ( vx cin cv wcel wa elin pm3.26 sylbi ssri ) CABDZACEZLFMAFZMBFZGNMABHNOI
      JK $.
      $([27-Apr-94]$)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of Takeuti and Zaring, p. 18. $)
  inss2 $p |- ( A i^i B ) (_ B $=
      ( cin wss incom sseq1i inss1 mpbi ) BACZBDABCZBDIJBBAEFBAGH $.
      $([27-Apr-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of Monk1, p. 26. $)
    ssin $p |- ( A (_ ( B i^i C ) <-> ( A (_ B /\ A (_ C ) ) $=
      ( cin wss wa sstr inss1 mpi inss2 jca wceq ineq3 inabs indist 3eqtr4g
      sseqin1 bi2an 3imtr4 impbi ) ABCDZEZABEZACEZFZUBUCUDUBUABEUCAUABGBCHIUBUA
      CEUDAUACGBCJIKAABDZLZAACDZLZFZAAUADZLUEUBUJAADUFUHDAUKAUFAUHMANABCOPUCUGU
      DUIABQACQRAUAQST $.
      $([8-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation. $)
    ssrin $p |- ( A (_ B -> ( A i^i C ) (_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal cin wss wa id imrand elin 3imtr4g 19.20i df-ss
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $([16-Aug-94]$)
  $}

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    zfauscl.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation.  Compare
       Exercise 4 of Takeuti and Zaring, p. 22. $)
    zfauscl   $p |- ( A i^i B ) e. V $=
      ( vx vy cin cv wceq wex wel wcel wa wb wal dfcleq elin bilbi bial bitr
      biex zfaus mpbir issetri ) DABFZDGZUDHZDIEDJZEGZAKUHBKZLZMZENZDIUFULDUFUG
      UHUDKZMZENULEUEUDOUNUKEUMUJUGUHABPQRSTUIEDACUAUBUC $.
  $}

  ${
    zfauscl2.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation. $)
    zfauscl2   $p |- ( B i^i A ) e. V $=
      ( cin cvv incom zfauscl eqeltr ) BADABDEBAFABCGH $.
      $([27-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Closed-form, generalized Separation Scheme. $)
    zfausclg $p |- ( A e. C -> ( A i^i B ) e. V ) $=
      ( vx cv cin cvv wcel wceq ineq1 cleq3d visset zfauscl vtoclg ) DEZBFZGHA
      BFZGHDACOAIPQGOABJKOBDLMN $.
      $([7-Apr-95]$)
  $}

  ${
    ssex.1 $e |- B e. V $.
    $( The subset of a set is also a set.  Exercise 3 of Takeuti and Zaring,
       p. 22. $)
    ssex  $p |- ( A (_ B -> A e. V ) $=
      ( wss cin wceq cvv wcel sseqin1 cleq3 zfauscl2 mpbiri sylbi ) ABDAABEZFZA
      GHZABIOPNGHANGJBACKLM $.
      $([27-Apr-94]$)
  $}

  ${
    ssexi.1 $e |- B e. V $.
    ssexi.2 $e |- A (_ B $.
    $( The subset of a set is also a set. $)
    ssexi   $p |- A e. V $=
      ( wss cvv wcel ssex ax-mp ) ABEAFGABCHDI $.
      $([9-Sep-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The subset of a set is also a set.  Exercise 3 of Takeuti and Zaring,
       p. 22 (generalized). $)
    ssexg  $p |- ( B e. C -> ( A (_ B -> A e. V ) ) $=
      ( vx cv wss cvv wcel wi wceq sseq2 birimd visset ssex vtoclg ) ADEZFZAGH
      ZIABFZRIDBCPBJQSRPBAKLAPDMNO $.
      $([14-Aug-94]$)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    zfausab.1 $e |- A e. V $.
    $( Separation Scheme in terms of an abstraction class. $)
    zfausab   $p |- { x | ( x e. A /\ ph ) } e. V $=
      ( cv wcel wa cab ssab ssexi ) BECFAGBHCDABCIJ $.
      $([8-Jun-94]$)
  $}

  ${
    $d x A $.
    zfausrab.1 $e |- A e. V $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    zfausrab   $p |- { x e. A | ph } e. V $=
      ( crab cvv wcel cv wa cab df-rab cleq3i zfausab mpbir ) ABCEZFGBHCGAIBJZF
      GOPFABCKLABCDMN $.
      $([19-Jul-96]$)
  $}



