$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for class difference. $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 birand biabdv df-dif 3eqtr4g ) ABE
      ZDFZAGZRCGHZIZDJRBGZTIZDJACKBCKQUAUCDQSUBTABRLMNDACODBCOP $.
      $([10-Feb-97]$)

    $( Equality theorem for class difference. $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 binegd biland biabdv df-dif
      3eqtr4g ) ABEZDFZCGZSAGZHZIZDJTSBGZHZIZDJCAKCBKRUCUFDRUBUETRUAUDABSLMNODC
      APDCBPQ $.
      $([10-Feb-97]$)
  $}

  ${
    difeq1i.1 $e |- A = B $.
    $( Inference adding difference to the right in a class equality. $)
    difeq1i   $p |- ( A \ C ) = ( B \ C )  $=
      ( wceq cdif difeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $([15-Nov-02]$)

    $( Inference adding difference to the left in a class equality. $)
    difeq2i   $p |- ( C \ A ) = ( C \ B )  $=
      ( wceq cdif difeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $([15-Nov-02]$)
  $}

  ${
    difeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding difference to the right in a class equality. $)
    difeq1d   $p |- ( ph -> ( A \ C ) = ( B \ C ) ) $=
      ( wceq cdif difeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([15-Nov-02]$)

    $( Deduction adding difference to the left in a class equality. $)
    difeq2d   $p |- ( ph -> ( C \ A ) = ( C \ B ) ) $=
      ( wceq cdif difeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([15-Nov-02]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    difeqri.1 $e |- ( ( x e. A /\ -. x e. B ) <-> x e. C ) $.
    $( Inference from membership to difference. $)
    difeqri $p |- ( A \ B ) = C $=
      ( cdif cv wcel wn wa cab df-dif bicom biabri eqtr4 ) BCFAGZBHPCHIJZAKDABC
      LQADQPDHEMNO $.
      $([8-Jan-04]$)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbdif.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbdif.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for class difference. $)
    hbdif $p |- ( y e. ( A \ B ) -> A. x y e. ( A \ B ) ) $=
      ( cv wcel wn wa wal cdif hbne hban eldif bial 3imtr4 ) BGZCHZRDHZIZJZUBAK
      RCDLHZUCAKSUAAETAFMNRCDOZUCUBAUDPQ $.
      $([3-Dec-03]$)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
    ( cdif wcel wn eldif pm3.26bd ) ABCDEABEACEFABCGH $.
    $([29-Apr-94]$)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
    ( cdif wcel wn eldif pm3.27bd ) ABCDEABEACEFABCGH $.
    $([3-May-94]$)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
    ( cdif wcel eldifn con2 ) ACBDEABEACBFG $.
    $([27-Jun-94]$)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
    ( wcel cdif wn wa eldif biimpr exp pm2.15d imp ) ABDZABCEDZFACDZMONMOFZNNMP
    GABCHIJKL $.
    $([28-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Double class difference.  Exercise 11 of [TakeutiZaring] p. 22. $)
    difdif $p |- ( A \ ( B \ A ) ) = A $=
      ( vx cdif cv wcel wn wa wi eldif bineg iman bitr4 bilan pm4.45im difeqri
      ) CABADZACEZAFZRQFZGZHSRBFZSIZHSUAUCSUAUBSGHZGUCTUDRBAJKUBSLMNSUBOMP $.
      $([23-Jan-04]$)

    $( Subclass relationship for class difference.  Exercise 14 of [TakeutiZaring]
       p. 22. $)
    difss $p |- ( A \ B ) (_ A $=
      ( vx cdif cv eldifi ssriv ) CABDACEABFG $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.
    $( Double complement under universal class.  Exercise 4.10(s) of
       [Mendelson] p. 231. $)
    difid $p |- ( V \ ( V \ A ) ) = A $=
      ( vx cvv cdif cv wcel wn wa eldif visset mpbiran bicon2 biantrur bitr2
      difeqri ) BCCADZABEZAFZQPFZGZQCFZTHSRSUARGQCAIBJZKLUATUBMNO $.
      $([9-Jan-04]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Contraposition law for subsets. $)
    ssconb $p |- ( ( A (_ C /\ B (_ C ) ->
                  ( A (_ ( C \ B ) <-> B (_ ( C \ A ) ) ) $=
      ( vx wss wa cv wcel cdif wi wal wn wb pm5.1 ssel syl2an bi2.03 a1i
      bi2and jcab 3bitr4g eldif bilim bialdv dfss2 ) ACEZBCEZFZDGZAHZUICBIZHZJZ
      DKUIBHZUICAIZHZJZDKAUKEBUOEUHUMUQDUHUJUICHZUNLZFZJZUNURUJLZFZJZUMUQUHUJUR
      JZUJUSJZFUNURJZUNVBJZFVAVDUHVEVGVFVHVEVGVEVGMUFUGVEVGNACUIOBCUIOPVFVHMUHU
      JUNQRSUJURUSTUNURVBTUAULUTUJUICBUBUCUPVCUNUICAUBUCUAUDDAUKUEDBUOUEUA $.
      $([22-Mar-98]$)

    $( Contraposition law for subsets.  Exercise 15 of [TakeutiZaring]
       p. 22. $)
    sscon $p |- ( A (_ B -> ( C \ B ) (_ ( C \ A ) ) $=
      ( vx wss cdif cv wcel wn wa ssel pm2.16d imland eldif 3imtr4g ssrdv ) ABE
      ZDCBFZCAFZQDGZCHZTBHZIZJUATAHZIZJTRHTSHQUCUEUAQUDUBABTKLMTCBNTCANOP $.
      $([22-Mar-98]$)

    $( Difference law for subsets. $)
    ssdif $p |- ( A (_ B -> ( A \ C ) (_ ( B \ C ) ) $=
      ( vx wss cdif cv wcel wn wa ssel imrand eldif 3imtr4g ssrdv ) ABEZDACFZBC
      FZPDGZAHZSCHIZJSBHZUAJSQHSRHPTUBUAABSKLSACMSBCMNO $.
      $([28-May-98]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( ( x e. A \/ x e. B ) <-> x e. C ) $.
    $( Inference from membership to union. $)
    uneqri   $p |- ( A u. B ) = C $=
      ( cun cv wcel wo cab dfun2 bicom biabri eqtr4 ) BCFAGZBHOCHIZAJDABCKPADPO
      DHELMN $.
      $([8-Jan-04]$)
  $}

  ${
    $d x A $.
    $( Idempotent law for union of classes.  Theorem 23 of [Suppes] p. 27. $)
    unidm $p |- ( A u. A ) = A $=
      ( vx cv wcel oridm uneqri ) BAAABCADEF $.
      $([8-Jan-04]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Commutative law for union of classes.  Exercise 6 of [TakeutiZaring]
       p. 17. $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cv wcel wo cab cun orcom biabi dfun2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGAB
      HBAHPQCNOIJCABKCBAKL $.
      $([25-Jun-98]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cv wcel wo cab cun eleq2 birord biabdv dfun2 3eqtr4g ) ABEZDFZA
      GZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
      $([5-Aug-93]$)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
    ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([5-Aug-93]$)

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i   $p |- ( A u. C ) = ( B u. C )  $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEDABCGH $.
      $([30-Aug-93]$)

    $( Inference adding union to the left in a class equality. $)
    uneq2i   $p |- ( C u. A ) = ( C u. B )  $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEDABCGH $.
      $([30-Aug-93]$)
  $}

  ${
    uneq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the right in a class equality. $)
    uneq1d   $p |- ( ph -> ( A u. C ) = ( B u. C ) ) $=
      ( wceq cun uneq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([29-Mar-98]$)

    $( Deduction adding union to the left in a class equality. $)
    uneq2d   $p |- ( ph -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([29-Mar-98]$)
  $}

  $( Equality theorem for union of two classes. $)
  uneq12 $p |- ( ( A = B /\ C = D ) -> ( A u. C ) = ( B u. D ) ) $=
    ( wceq cun uneq1 uneq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $([29-Mar-98]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in class union.  Theorem 12 of [Suppes] p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cun wcel cvv wo elisset jaoi cv wceq eleq1 bi2ord dfun2 vtoclabg
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARITSUAABIACIJDKZBFZUCCFZHUBDARGUCALUDTUE
      UAUCABMUCACMNDBCOPQ $.
      $([7-Aug-94]$)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbun.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbun.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the union of classes. $)
    hbun $p |- ( y e. ( A u. B ) -> A. x y e. ( A u. B ) ) $=
      ( cv wcel wo wal cun hbor elun bial 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRAE
      FLPCDMZTSAUANO $.
      $([15-Sep-03]$)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for union of classes.  Exercise 8 of [TakeutiZaring]
       p. 17. $)
    unass $p |- ( ( A u. B ) u. C ) = ( A u. ( B u. C ) ) $=
      ( vx cun cv wcel wo orass elun biror bilor 3bitr4 bitr4 uneqri ) DABEZCAB
      CEZEZDFZPGZSCGZHZSAGZSQGZHZSRGUCSBGZHZUAHUCUFUAHZHUBUEUCUFUAITUGUASABJKUD
      UHUCSBCJLMSAQJNO $.
      $([3-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass relationship for union of classes.  Theorem 25 of [Suppes]
       p. 27. $)
    ssun1 $p |- A (_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssriv ) CAABDZCEZAFZNMBFZGMLFNOHMABIJ
      K $.
      $([5-Aug-93]$)
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A (_ ( B u. A ) $=
    ( cun ssun1 uncom sseqtr ) AABCBACABDABEF $.
    $([30-Aug-93]$)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A (_ B -> A (_ ( B u. C ) ) $=
    ( wss cun ssun1 sstr mpi ) ABDBBCEZDAIDBCFABIGH $.
    $([5-Aug-93]$)

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A (_ B -> A (_ ( C u. B ) ) $=
    ( wss cun ssun2 sstr mpi ) ABDBCBEZDAIDBCFABIGH $.
    $([14-Aug-94]$)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
    ( cun ssun1 ssi ) BBCDABCEF $.
    $([5-Aug-93]$)

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
    ( cun ssun2 ssi ) BCBDABCEF $.
    $([30-Aug-93]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Subclass law for union of classes. $)
    unss1 $p |- ( A (_ B -> ( A u. C ) (_ ( B u. C ) ) $=
      ( vx cv wcel wi wal cun wss wo id imrord elun 3imtr4g 19.20i dfss2
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $([14-Oct-99]$)

    $( A relationship between subclass and union.  Theorem 26 of [Suppes]
       p. 27. $)
    ssequn1 $p |- ( A (_ B <-> B = ( A u. B ) ) $=
      ( vx wss cv wcel wo cab wceq cun wi wal wb pm4.72 bial dfss2 cleqab
      3bitr4 dfun2 cleq2i bitr4 ) ABDZBCEZAFZUCBFZGZCHZIZBABJZIUDUEKZCLUEUFMZCL
      UBUHUJUKCUDUENOCABPUFCBQRUIUGBCABSTUA $.
      $([30-Aug-93]$)
  $}

  $( Subclass law for union of classes.  Exercise 7 of [TakeutiZaring]
     p. 18. $)
  unss2 $p |- ( A (_ B -> ( C u. A ) (_ ( C u. B ) ) $=
    ( wss cun unss1 uncom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([14-Oct-99]$)

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A (_ B <-> B = ( B u. A ) ) $=
    ( wss cun wceq ssequn1 uncom cleq2i bitr ) ABCBABDZEBBADZEABFJKBABGHI $.
    $([13-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( The union of two subclasses is a subclass.  Theorem 27 of [Suppes] p. 27
       and its converse. $)
    unss $p |- ( ( A u. B ) (_ C <-> ( A (_ C /\ B (_ C ) ) $=
      ( vx cv cun wcel wi wal wa wss wo elun birim jaob bitr bial dfss2 bi2an
      19.26 bitr4 3bitr4 ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZUDCKACKZBCK
      ZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUMUNURUOUSDAC
      RDBCRSUIUKDTUAUB $.
      $([4-Jun-94]$)
  $}

  ${
    unssi.1 $e |- A (_ C $.
    unssi.2 $e |- B (_ C $.
    $( An inference that the union of two subclasses is a subclass.
       Contributed by Raph Levien 10-Dec-02. $)
    unssi $p |- ( A u. B ) (_ C $=
      ( cun wss wa pm3.2i unss mpbir ) ABFCGACGZBCGZHLMDEIABCJK $.
      $([14-May-03]$)
  $}

  $( A condition that implies inclusion in the union of two classes. $)
  ssun $p |- ( ( A (_ B \/ A (_ C ) -> A (_ ( B u. C ) ) $=
    ( wss cun ssun3 ssun4 jaoi ) ABDABCEDACDABCFACBGH $.
    $([23-Nov-03]$)

  $( The subclass relationship is inherited by the union of classes. $)
  ss2un $p |- ( ( A (_ C /\ B (_ D ) -> ( A u. B ) (_ ( C u. D ) ) $=
    ( wss wa cun ssun3 ssun4 im2an unss sylibr ) ACEZBDEZFACDGZEZBOEZFABGOEMPNQ
    ACDHBDCIJABOKL $.
    $([23-Nov-03]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Absorption of difference by union.  This decomposes a union into two
       disjoint classes (see ~ indif0 ).  Part of proof of Corollary 6K of
       [Enderton] p. 144. $)
    undif1 $p |- ( A u. B ) = ( A u. ( B \ A ) ) $=
      ( vx cun cdif cv wcel wo wn wa wi abai pm4.1 iman bitr3 bilan bitr bineg
      oran 3bitr4 elun eldif bilor cleqri ) CABDZABAEZDZCFZAGZUHBGZHZUIUJUIIZJZ
      HZUHUEGUHUGGZULUJIZJZIULUMIZJZIUKUNUQUSUQULULUPKZJUSULUPLUTURULUTUJUIKURU
      JUIMUJUINOPQRUIUJSUIUMSTUHABUAUOUIUHUFGZHUNUHAUFUAVAUMUIUHBAUBUCQTUD $.
      $([19-May-98]$)

    $( Absorption of union by difference.  Theorem 36 of [Suppes] p. 29. $)
    undif2 $p |- ( A \ B ) = ( ( A u. B ) \ B ) $=
      ( vx cdif cun cv wcel wn wa wo andir elun biran pm3.24 biorfi 3bitr4r
      eldif 3bitr4 cleqri ) CABDZABEZBDZCFZAGZUCBGZHZIZUCUAGZUFIZUCTGUCUBGUDUEJ
      ZUFIUGUEUFIZJUIUGUDUEUFKUHUJUFUCABLMUKUGUENOPUCABQUCUABQRS $.
      $([19-May-98]$)

    $( Union of complementary parts into whole. $)
    undif3 $p |- ( A (_ B <-> B = ( A u. ( B \ A ) ) ) $=
      ( wss cun wceq cdif ssequn1 undif1 cleq2i bitr ) ABCBABDZEBABAFDZEABGKLBA
      BHIJ $.
      $([22-Mar-98]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    $( Commutative law for intersection of classes.  Exercise 7 of [TakeutiZaring]
       p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cv wcel wa cab cin ancom biabi dfin2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGAB
      HBAHPQCNOIJCABKCBAKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( ( x e. A /\ x e. B ) <-> x e. C ) $.
    $( Inference from membership to intersection. $)
    ineqri $p |- ( A i^i B ) = C $=
      ( cin cv wcel wa cab dfin2 bicom biabri eqtr4 ) BCFAGZBHOCHIZAJDABCKPADPO
      DHELMN $.
      $([10-Jan-04]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cv wcel wa cab cin eleq2 birand biabdv dfin2 3eqtr4g ) ABEZDFZA
      GZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
      $([14-Dec-93]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
    ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([26-Dec-93]$)

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i   $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEDABCGH $.
      $([26-Dec-93]$)

    $( Equality inference for intersection of two classes. $)
    ineq2i   $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEDABCGH $.
      $([26-Dec-93]$)
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d   $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([10-Apr-94]$)

    $( Equality deduction for intersection of two classes. $)
    ineq2d   $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([10-Apr-94]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq12 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
    ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $([8-May-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of [Suppes] p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cin wcel cvv wa elisset adantl cv wceq eleq1 bi2and dfin2 vtoclabg
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARIUASTACIJDKZBFZUCCFZHUBDARGUCALUDTUEUAU
      CABMUCACMNDBCOPQ $.
      $([29-Apr-94]$)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbin.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbin.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the intersection of classes. $)
    hbin $p |- ( y e. ( A i^i B ) -> A. x y e. ( A i^i B ) ) $=
      ( cv wcel wa wal cin hban elin bial 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRAE
      FLPCDMZTSAUANO $.
      $([15-Sep-03]$)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Union of two class abstractions. $)
    unab $p |- ( { x | ph } u. { x | ps } ) = { x | ( ph \/ ps ) } $=
      ( vy cab cun wo cv wcel wsb df-clab bi2or sbo bitr4 elun 3bitr4 cleqri )
      DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACDJZUCTIUCUBIUFACDJZBCDJZGUGUDUHUEUIADCK
      BDCKLABCDMNUCRSOUADCKPQ $.
      $([29-Sep-02]$)

    $( Intersection of two class abstractions. $)
    inab $p |- ( { x | ph } i^i { x | ps } ) = { x | ( ph /\ ps ) } $=
      ( vy cab cin wa cv wcel wsb df-clab bi2an sba bitr4 elin 3bitr4 cleqri )
      DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACDJZUCTIUCUBIUFACDJZBCDJZGUGUDUHUEUIADCK
      BDCKLABCDMNUCRSOUADCKPQ $.
      $([29-Sep-02]$)
  $}

  ${
    $d x A $.
    $( Alternate definition of restricted abstraction class. $)
    dfrab2 $p |- { x e. A | ph } = ( { x | ph } i^i A ) $=
      ( crab cv wcel wa cab cin df-rab inab abid2 ineq1i eqtr3 incom 3eqtr ) AB
      CDBECFZAGBHZCABHZIZSCIABCJQBHZSIRTQABKUACSBCLMNCSOP $.
      $([20-Sep-03]$)
  $}

  ${
    $d x A $.
    $( Idempotent law for intersection of classes.  Theorem 15 of [Suppes]
       p. 26. $)
    inidm $p |- ( A i^i A ) = A $=
      ( vx cv wcel anidm ineqri ) BAAABCADEF $.
      $([9-Jan-04]$)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for intersection of classes.  Exercise 9 of [TakeutiZaring]
       p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa anass elin bilan bitr4 biran 3bitr4 ineqri ) DABEZCAB
      CEZEZDFZAGZSBGZHZSCGZHZTSQGZHZSPGZUCHSRGUDTUAUCHZHUFTUAUCIUEUHTSBCJKLUGUB
      UCSABJMSAQJNO $.
      $([3-May-94]$)
  $}

  $( A rearrangement of intersection. $)
  in12 $p |- ( A i^i ( B i^i C ) ) = ( B i^i ( A i^i C ) ) $=
    ( cin incom ineq1i inass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $([21-Apr-01]$)

  $( A rearrangement of intersection. $)
  in23 $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i B ) $=
    ( cin incom ineq2i inass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $([21-Apr-01]$)

  $( Rearrangement of intersection of 4 classes. $)
  in4 $p |- ( ( A i^i B ) i^i ( C i^i D ) ) =
            ( ( A i^i C ) i^i ( B i^i D ) ) $=
    ( cin in12 ineq2i inass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $([21-Apr-01]$)

  $( Intersection distributes over itself. $)
  indist $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
    ( cin inidm ineq1i in4 eqtr3 ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $([6-May-94]$)

  $( A relationship between subclass and intersection.  Similar to
     Exercise 9 of [TakeutiZaring] p. 18. $)
  sseqin2 $p |- ( A (_ B <-> A = ( B i^i A ) ) $=
    ( wss cin wceq df-ss incom cleq2i bitr ) ABCAABDZEABADZEABFJKAABGHI $.
    $([17-May-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of [TakeutiZaring] p. 18. $)
    inss1 $p |- ( A i^i B ) (_ A $=
      ( vx cin cv wcel elin pm3.26bd ssriv ) CABDZACEZJFKAFKBFKABGHI $.
      $([27-Apr-94]$)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of [TakeutiZaring] p. 18. $)
  inss2 $p |- ( A i^i B ) (_ B $=
    ( cin incom inss1 eqsstr3 ) ABCBACBBADBAEF $.
    $([27-Apr-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of [Monk1] p. 26. $)
    ssin $p |- ( A (_ ( B i^i C ) <-> ( A (_ B /\ A (_ C ) ) $=
      ( cin wss wa inss1 sstr mpi inss2 jca wceq ineq12 indist syl6eqr inidm
      syl5eqr df-ss bi2an 3imtr4 impbi ) ABCDZEZABEZACEZFZUCUDUEUCUBBEUDBCGAUBB
      HIUCUBCEUEBCJAUBCHIKAABDZLZAACDZLZFZAAUBDZLUFUCUKAADZULAUKUMUGUIDULAUGAUI
      MABCNOAPQUDUHUEUJABRACRSAUBRTUA $.
      $([8-May-94]$)
  $}

  ${
    ssini.1 $e |- A (_ B $.
    ssini.2 $e |- A (_ C $.
    $( An inference showing that the a subclass of two classes is a subclass of
       their intersection. $)
    ssini $p |- A (_ ( B i^i C ) $=
      ( cin wss wa pm3.2i ssin mpbir ) ABCFGABGZACGZHLMDEIABCJK $.
      $([24-Nov-03]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation. $)
    ssrin $p |- ( A (_ B -> ( A i^i C ) (_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal cin wss wa id imrand elin 3imtr4g 19.20i dfss2
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $([16-Aug-94]$)

    $( Add left intersection to subclass relation. $)
    sslin $p |- ( A (_ B -> ( C i^i A ) (_ ( C i^i B ) ) $=
      ( wss cin ssrin incom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $([19-Oct-99]$)
  $}

  $( Intersection of subclasses. $)
  ss2in $p |- ( ( A (_ B /\ C (_ D ) -> ( A i^i C ) (_ ( B i^i D ) ) $=
    ( cin wss sstr2 ssrin sslin syl2an ) ACEZBCEZFLBDEZFKMFABFCDFKLMGABCHCDBIJ
    $.
    $([5-May-00]$)

  $( Intersection preserves subclass relationship. $)
  ssinss1 $p |- ( A (_ C -> ( A i^i B ) (_ C ) $=
    ( cin wss wi inss1 sstr ax-mp ) ABDZAEACEJCEFABGJACHI $.
    $([14-Sep-99]$)

  ${
    $d x A $.  $d x B $.
    $( Subclass defined in terms of class difference.  This is one of many ways
       to express the subclass relation in terms of other symbols, including
       ~ dfss2 , ~ dfss3 , ~ sspss , ~ df-ss , ~ ssequn1 , and ~ ssdif0 .  The
       difference operation could be considered a primitive operation for a
       kind of "set algebra" where it can also define union ~ df-un ,
       intersection ~ df-in , and the empty set ~ df-nul . $)
    dfss4 $p |- ( A (_ B <-> A = ( B \ ( B \ A ) ) ) $=
      ( vx wss cin wceq cdif sseqin2 cv wcel wa wn wi abai iman bilan bitr
      elin eldif bineg 3bitr4 cleqri cleq2i ) ABDABAEZFABBAGZGZFABHUDUFACUDUFCI
      ZBJZUGAJZKZUHUHUILKZLZKZUGUDJUGUFJZUJUHUHUIMZKUMUHUINUOULUHUHUIOPQUGBARUN
      UHUGUEJZLZKUMUGBUESUQULUHUPUKUGBASTPQUAUBUCQ $.
      $([22-Mar-98]$)

    $( Difference with intersection.  Theorem 33 of [Suppes] p. 29. $)
    difin $p |- ( A \ B ) = ( A \ ( A i^i B ) ) $=
      ( vx cdif cin cv wcel wn wa wi abai imnan bilan bitr eldif elin bineg
      3bitr4 cleqri ) CABDZAABEZDZCFZAGZUCBGZHZIZUDUDUEIZHZIZUCTGUCUBGZUGUDUDUF
      JZIUJUDUFKULUIUDUDUELMNUCABOUKUDUCUAGZHZIUJUCAUAOUNUIUDUMUHUCABPQMNRS $.
      $([31-Mar-98]$)
  $}

  $( Union defined in terms of intersection; analogous to DeMorgan's
     law.  Definition of [Mendelson] p. 231. $)
  dfun3 $p |- ( A u. B ) = ( V \ ( ( V \ A ) i^i ( V \ B ) ) ) $=
    ( cun cvv cdif cin df-un df-in difid difeq2i eqtr2 eqtr ) ABCDDAEZBEZEDMDBE
    ZFZEABGNPDPMDOEZENMOHQBMBIJKJL $.
    $([8-Jan-02]$)

  $( Intersection defined in terms of union; analogous to DeMorgan's
     law.  Similar to Exercise 4.10(n) of [Mendelson] p. 231. $)
  dfin3 $p |- ( A i^i B ) = ( V \ ( ( V \ A ) u. ( V \ B ) ) ) $=
    ( cvv cdif cun cin difid df-un difeq1i difeq2i eqtr df-in 3eqtr4r ) CCACBDZ
    DZDZDOCCADZNEZDABFOGRPCRCCQDZNDZDPQNHTOCSANAGIJKJABLM $.
    $([8-Jan-02]$)

  $( Alternate definition of the union of two classes.  Exercise 4.10(q) of
     [Mendelson] p. 231. $)
  dfin4 $p |- ( A i^i B ) = ( A \ ( A \ B ) ) $=
    ( cin cdif wss wceq inss1 dfss4 mpbi difin difeq2i eqtr4 ) ABCZAAMDZDZAABDZ
    DMAEMOFABGMAHIPNAABJKL $.
    $([25-Nov-03]$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Distributive law for intersection over union.  Exercise 10 of
       [TakeutiZaring] p. 17. $)
    inundist $p |- ( A i^i ( B u. C ) ) = ( ( A i^i B ) u. ( A i^i C ) ) $=
      ( vx cun cin cv wcel wa wo andi elun bilan elin bi2or 3bitr4 cleqri ) DAB
      CEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJIZUD
      UKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $([30-Sep-02]$)

    $( Distributive law for union over intersection.  Exercise 11 of
       [TakeutiZaring] p. 17. $)
    unindist $p |- ( A u. ( B i^i C ) ) = ( ( A u. B ) i^i ( A u. C ) ) $=
      ( vx cin cun cv wcel wo wa ordi elin bilor elun bi2an 3bitr4 cleqri ) DAB
      CEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJIZUD
      UKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $([30-Sep-02]$)
  $}

  $( Distributive law for intersection over union.  Theorem 28 of [Suppes]
     p. 27. $)
  inundistr $p |- ( ( A u. B ) i^i C ) = ( ( A i^i C ) u. ( B i^i C ) ) $=
    ( cun cin inundist incom wceq uneq12 mp2an 3eqtr4 ) CABDZECAEZCBEZDZLCEACEZ
    BCEZDZCABFLCGPMHQNHROHACGBCGPMQNIJK $.
    $([30-Sep-02]$)

  $( Distributive law for union over intersection.  Theorem 29 of [Suppes]
     p. 27. $)
  unindistr $p |- ( ( A i^i B ) u. C ) = ( ( A u. C ) i^i ( B u. C ) ) $=
    ( cin cun unindist uncom wceq ineq12 mp2an 3eqtr4 ) CABDZECAEZCBEZDZLCEACEZ
    BCEZDZCABFLCGPMHQNHROHACGBCGPMQNIJK $.
    $([30-Sep-02]$)

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    zfauscl.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation.  Compare
       Exercise 4 of [TakeutiZaring] p. 22. $)
    zfauscl   $p |- ( A i^i B ) e. V $=
      ( vx vy cin cv wceq wex wel wcel wa wb wal zfaus dfcleq elin bilbi bial
      bitr biex mpbir issetri ) DABFZDGZUDHZDIEDJZEGZAKUHBKZLZMZENZDIUIEDACOUFU
      LDUFUGUHUDKZMZENULEUEUDPUNUKEUMUJUGUHABQRSTUAUBUC $.
      $([5-Aug-93]$)
  $}

  ${
    zfauscl2.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation. $)
    zfauscl2   $p |- ( B i^i A ) e. V $=
      ( cin cvv incom zfauscl eqeltr ) BADABDEBAFABCGH $.
      $([27-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Closed-form, generalized Separation Scheme. $)
    zfausclg $p |- ( A e. C -> ( A i^i B ) e. V ) $=
      ( vx cv cin cvv wcel wceq ineq1 eleq1d visset zfauscl vtoclg ) DEZBFZGHAB
      FZGHDACOAIPQGOABJKOBDLMN $.
      $([7-Apr-95]$)
  $}

  ${
    ssex.1 $e |- B e. V $.
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring]
       p. 22.  This is one way of expressing the Axiom of Separation ~ zfaus
       (a.k.a. Subset Axiom). $)
    ssex  $p |- ( A (_ B -> A e. V ) $=
      ( wss cin wceq cvv wcel df-ss zfauscl2 eleq1 mpbiri sylbi ) ABDAABEZFZAGH
      ZABIOPNGHBACJANGKLM $.
      $([27-Apr-94]$)
  $}

  ${
    ssexi.1 $e |- B e. V $.
    ssexi.2 $e |- A (_ B $.
    $( The subset of a set is also a set. $)
    ssexi   $p |- A e. V $=
      ( wss cvv wcel ssex ax-mp ) ABEAFGDABCHI $.
      $([9-Sep-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring]
       p. 22 (generalized). $)
    ssexg  $p |- ( B e. C -> ( A (_ B -> A e. V ) ) $=
      ( vx cv wss cvv wcel wi wceq sseq2 birimd visset ssex vtoclg ) ADEZFZAGHZ
      IABFZRIDBCPBJQSRPBAKLAPDMNO $.
      $([14-Aug-94]$)
  $}

  $( Existence of a difference. $)
  difexg $p |- ( A e. C -> ( A \ B ) e. V ) $=
    ( wcel cdif wss cvv difss ssexg mpi ) ACDABEZAFKGDABHKACIJ $.
    $([26-May-98]$)

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    zfausab.1 $e |- A e. V $.
    $( Separation Scheme in terms of an abstraction class. $)
    zfausab   $p |- { x | ( x e. A /\ ph ) } e. V $=
      ( cv wcel wa cab ssab ssexi ) BECFAGBHCDABCIJ $.
      $([8-Jun-94]$)
  $}

  ${
    $d x A $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    zfausrabg $p |- ( A e. B -> { x e. A | ph } e. V ) $=
      ( wcel crab wss cvv ssrab ssexg mpi ) CDEABCFZCGLHEABCILCDJK $.
      $([23-Oct-99]$)
  $}

  ${
    $d x A $.
    zfausrab.1 $e |- A e. V $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    zfausrab   $p |- { x e. A | ph } e. V $=
      ( cvv wcel crab zfausrabg ax-mp ) CEFABCGEFDABCEHI $.
      $([19-Jul-96]$)
  $}

