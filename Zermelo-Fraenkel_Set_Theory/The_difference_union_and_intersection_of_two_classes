$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols. $)
  $c \ $. $( Backslash (difference) $)
  $c u. $. $( Cup (union) $)
  $c i^i $. $( Cap (intersection) $)

  $( Extend class notation to include class difference (read:
     " ` A ` minus ` B ` "). $)
  cdif $a class ( A \ B ) $.

  $( Extend class notation to include union of two classes (read:  " ` A `
     union ` B ` "). $)
  cun $a class ( A u. B ) $.

  $( Extend class notation to include intersection of two classes (read:
     " ` A ` intersect ` B ` "). $)
  cin $a class ( A i^i B ) $.

  ${
    $d x A $.  $d x B $.
    $( Define class difference.  Definition 5.12 of Takeuti and Zaring,
       p. 20.  Several notations are used in the literature; we chose the ` \ `
       convention (used in Suppes) instead of a minus sign to reserve the
       latter for later use in, e.g., arithmetic. $)
    df-dif $a |- ( A \ B ) = { x | ( x e. A /\ -. x e. B ) } $.
  $}

  $( Define union of two classes.  For a more "typical" definition, but
     requiring a dummy variable, see ~ dfun2 . $)
  df-un $a |- ( A u. B ) = ( V \ ( ( V \ A ) \ B ) ) $.

  $( Define intersection of two classes.  For a more "typical" definition,
     but requiring a dummy variable, see ~ dfin2 . $)
  df-in $a |- ( A i^i B ) = ( A \ ( V \ B ) ) $.

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for class difference. $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 birand biabdv df-dif 3eqtr4g ) ABE
      ZDFZAGZRCGHZIZDJRBGZTIZDJACKBCKQUAUCDQSUBTABRLMNDACODBCOP $.
      $([10-Feb-97]$)

    $( Equality theorem for class difference. $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 binegd biland biabdv df-dif
      3eqtr4g ) ABEZDFZCGZSAGZHZIZDJTSBGZHZIZDJCAKCBKRUCUFDRUBUETRUAUDABSLMNODC
      APDCBPQ $.
      $([10-Feb-97]$)

    $( Expansion of membership in a class difference. $)
    eldif $p |- ( A e. ( B \ C ) <-> ( A e. B /\ -. A e. C ) ) $=
      ( vx cdif wcel cvv wn wa elisset adantr cv wceq eleq1 binegd bi2and
      df-dif vtoclabg pm5.21nii ) ABCEZFAGFZABFZACFZHZIZATJUBUAUDABJKDLZBFZUFCF
      ZHZIUEDATGUFAMZUGUBUIUDUFABNUJUHUCUFACNOPDBCQRS $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    difeqri.1 $e |- ( x e. A <-> ( x e. B /\ -. x e. C ) ) $.
    $( Inference from membership to difference. $)
    difeqri   $p |- A = ( B \ C ) $=
      ( cv wcel wn wa cab cdif cleqabri df-dif eqtr4 ) BAFZCGODGHIZAJCDKPABELAC
      DMN $.
      $([17-May-98]$)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
    ( cdif wcel wn eldif pm3.26bd ) ABCDEABEACEFABCGH $.
    $([29-Apr-94]$)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
    ( cdif wcel wn eldif pm3.27bd ) ABCDEABEACEFABCGH $.
    $([3-May-94]$)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
    ( cdif wcel eldifn con2 ) ACBDEABEACBFG $.
    $([27-Jun-94]$)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
    ( wcel cdif wn wa eldif biimpr exp pm2.15d imp ) ABDZABCEDZFACDZMONMOFZNNMP
    GABCHIJKL $.
    $([28-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Double difference.  Exercise 11 of Takeuti and Zaring, p. 22. $)
    difdif $p |- A = ( A \ ( B \ A ) ) $=
      ( vx cdif cv wcel wi wa wn pm4.45im eldif bineg iman bitr4 bilan difeqri
      ) CAABADZCEZAFZSRBFZSGZHSRQFZIZHSTJUCUASUCTSIHZIUAUBUDRBAKLTSMNONP $.
      $([17-May-98]$)

    $( Subclass relationship for class difference.  Exercise 14 of Takeuti
       and Zaring, p. 22. $)
    difss $p |- ( A \ B ) (_ A $=
      ( vx cdif cv eldifi ssriv ) CABDACEABFG $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Contraposition law for subsets. $)
    ssconb $p |- ( ( A (_ C /\ B (_ C ) ->
                  ( A (_ ( C \ B ) <-> B (_ ( C \ A ) ) ) $=
      ( vx wss wa cv wcel cdif wi wal wn wb pm5.1 ssel syl2an bi2.03 a1i
      bi2and jcab 3bitr4g eldif bilim bialdv df-ss ) ACEZBCEZFZDGZAHZUICBIZHZJZ
      DKUIBHZUICAIZHZJZDKAUKEBUOEUHUMUQDUHUJUICHZUNLZFZJZUNURUJLZFZJZUMUQUHUJUR
      JZUJUSJZFUNURJZUNVBJZFVAVDUHVEVGVFVHVEVGVEVGMUFUGVEVGNACUIOBCUIOPVFVHMUHU
      JUNQRSUJURUSTUNURVBTUAULUTUJUICBUBUCUPVCUNUICAUBUCUAUDDAUKUEDBUOUEUA $.
      $([22-Mar-98]$)

    $( Contraposition law for subsets.  Exercise 15 of Takeuti and Zaring,
       p. 22. $)
    sscon $p |- ( A (_ B -> ( C \ B ) (_ ( C \ A ) ) $=
      ( vx wss cdif cv wcel wn wa ssel pm2.16d imland eldif 3imtr4g ssrdv ) ABE
      ZDCBFZCAFZQDGZCHZTBHZIZJUATAHZIZJTRHTSHQUCUEUAQUDUBABTKLMTCBNTCANOP $.
      $([22-Mar-98]$)

    $( Difference law for subsets. $)
    ssdif $p |- ( A (_ B -> ( A \ C ) (_ ( B \ C ) ) $=
      ( vx wss cdif cv wcel wn wa ssel imrand eldif 3imtr4g ssrdv ) ABEZDACFZBC
      FZPDGZAHZSCHIZJSBHZUAJSQHSRHPTUBUAABSKLSACMSBCMNO $.
      $([28-May-98]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Alternate definition of union of two classes.  Definition 5.6 of
       Takeuti and Zaring, p. 16. $)
    dfun2 $p |- ( A u. B ) = { x | ( x e. A \/ x e. B ) } $=
      ( cv wcel wo cun cvv cdif wn eldif visset mpbiran df-un eleq2i wa biran
      ioran 3bitr4 bicon2 cleqabri ) ADZBEZUBCEZFZABCGZUBHHBIZCIZIZEZUBUHEZJZUB
      UFEUEUJUBHEZULUBHUHKALZMUFUIUBBCNOUKUEUBUGEZUDJZPUCJZUPPUKUEJUOUQUPUOUMUQ
      UBHBKUNMQUBUGCKUCUDRSTSUA $.
      $([26-Dec-96]$)

    $( Alternate definition of intersection of two classes.  Definition 5.6
       of Takeuti and Zaring, p. 16. $)
    dfin2 $p |- ( A i^i B ) = { x | ( x e. A /\ x e. B ) } $=
      ( cv wcel wa cin cvv cdif wn eldif df-in eleq2i visset mpbiran bicon2
      bilan 3bitr4 cleqabri ) ADZBEZTCEZFZABCGZTBHCIZIZEUATUEEZJZFTUDEUCTBUEKUD
      UFTBCLMUBUHUAUGUBUGTHEUBJTHCKANOPQRS $.
      $([26-Dec-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( x e. A <-> ( x e. B \/ x e. C ) ) $.
    $( Inference from membership to union. $)
    uneqri   $p |- A = ( B u. C ) $=
      ( cv wcel wo cab cun cleqabri dfun2 eqtr4 ) BAFZCGNDGHZAICDJOABEKACDLM $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Absorption law for union of classes.  Theorem 23 of Suppes, p. 27. $)
    unabs $p |- A = ( A u. A ) $=
      ( vx cv wcel orabs uneqri ) BAAABCADEF $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Commutative law for union of classes.  Exercise 6 of Takeuti and Zaring,
       p. 17. $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cv wcel wo cab cun orcom biab dfun2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGABH
      BAHPQCNOIJCABKCBAKL $.
      $([25-Jun-98]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cv wcel wo cab cun eleq2 birord biabdv dfun2 3eqtr4g ) ABEZDFZA
      GZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
      $([5-Aug-93]$)
  $}

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i   $p |- ( A u. C ) = ( B u. C )  $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEDABCGH $.
      $([30-Aug-93]$)
  $}

  ${
    uneq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the right in a class equality. $)
    uneq1d   $p |- ( ph -> ( A u. C ) = ( B u. C ) ) $=
      ( wceq cun uneq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([29-Mar-98]$)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
    ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([5-Aug-93]$)

  ${
    uneq2i.1 $e |- A = B $.
    $( Inference adding union to the left in a class equality. $)
    uneq2i   $p |- ( C u. A ) = ( C u. B )  $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEDABCGH $.
      $([30-Aug-93]$)
  $}

  ${
    uneq2d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the left in a class equality. $)
    uneq2d   $p |- ( ph -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([29-Mar-98]$)
  $}

  $( Equality theorem for union of two classes. $)
  uneq12 $p |- ( ( A = B /\ C = D ) -> ( A u. C ) = ( B u. D ) ) $=
    ( wceq cun uneq1 uneq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $([29-Mar-98]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in class union.  Theorem 12 of Suppes, p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cun wcel cvv wo elisset jaoi cv wceq eleq1 bi2ord dfun2 vtoclabg
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARITSUAABIACIJDKZBFZUCCFZHUBDARGUCALUDTUE
      UAUCABMUCACMNDBCOPQ $.
      $([7-Aug-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass relationship for union of classes.  Theorem 25 of Suppes,
       p. 27. $)
    ssun1 $p |- A (_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssriv ) CAABDZCEZAFZNMBFZGMLFNOHMABIJK
      $.
      $([5-Aug-93]$)
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A (_ ( B u. A ) $=
    ( cun ssun1 uncom sseqtr ) AABCBACABDABEF $.
    $([30-Aug-93]$)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A (_ B -> A (_ ( B u. C ) ) $=
    ( wss cun ssun1 sstr mpi ) ABDBBCEZDAIDBCFABIGH $.
    $([5-Aug-93]$)

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A (_ B -> A (_ ( C u. B ) ) $=
    ( wss cun ssun2 sstr mpi ) ABDBCBEZDAIDBCFABIGH $.
    $([14-Aug-94]$)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
    ( cun ssun1 ssi ) BBCDABCEF $.
    $([5-Aug-93]$)

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
    ( cun ssun2 ssi ) BCBDABCEF $.
    $([30-Aug-93]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Subclass law for union of classes. $)
    unss1 $p |- ( A (_ B -> ( A u. C ) (_ ( B u. C ) ) $=
      ( vx cv wcel wi wal cun wss wo id imrord elun 3imtr4g 19.20i df-ss
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $([14-Oct-99]$)

    $( A relationship between subclass and union.  Theorem 26 of Suppes,
       p. 27. $)
    ssequn1 $p |- ( A (_ B <-> B = ( A u. B ) ) $=
      ( vx wss cv wcel wo cab wceq cun wi wal wb pm4.72 bial df-ss cleqab
      3bitr4 dfun2 cleq2i bitr4 ) ABDZBCEZAFZUCBFZGZCHZIZBABJZIUDUEKZCLUEUFMZCL
      UBUHUJUKCUDUENOCABPUFCBQRUIUGBCABSTUA $.
      $([30-Aug-93]$)
  $}

  $( Subclass law for union of classes. $)
  unss2 $p |- ( A (_ B -> ( C u. A ) (_ ( C u. B ) ) $=
    ( wss cun unss1 uncom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([14-Oct-99]$)

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A (_ B <-> B = ( B u. A ) ) $=
    ( wss cun wceq ssequn1 uncom cleq2i bitr ) ABCBABDZEBBADZEABFJKBABGHI $.
    $([13-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Union of subclasses. $)
    unss $p |- ( ( A u. B ) (_ C <-> ( A (_ C /\ B (_ C ) ) $=
      ( vx cv cun wcel wi wal wa wss wo elun birim jaob bitr bial df-ss bi2an
      19.26 bitr4 3bitr4 ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZUDCKACKZBCK
      ZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUMUNURUOUSDAC
      RDBCRSUIUKDTUAUB $.
      $([4-Jun-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Absorption of difference by union. $)
    undif1 $p |- ( A u. B ) = ( A u. ( B \ A ) ) $=
      ( vx cun cdif cv wcel wo wn wa wi abai pm4.1 iman bitr3 bilan bitr bineg
      oran 3bitr4 elun eldif bilor cleqri ) CABDZABAEZDZCFZAGZUHBGZHZUIUJUIIZJZ
      HZUHUEGUHUGGZULUJIZJZIULUMIZJZIUKUNUQUSUQULULUPKZJUSULUPLUTURULUTUJUIKURU
      JUIMUJUINOPQRUIUJSUIUMSTUHABUAUOUIUHUFGZHUNUHAUFUAVAUMUIUHBAUBUCQTUD $.
      $([19-May-98]$)

    $( Absorption of union by difference. $)
    undif2 $p |- ( A \ B ) = ( ( A u. B ) \ B ) $=
      ( vx cdif cun cv wcel wn wa wo andir elun biran pm3.24 biorfi 3bitr4r
      eldif 3bitr4 cleqri ) CABDZABEZBDZCFZAGZUCBGZHZIZUCUAGZUFIZUCTGUCUBGUDUEJ
      ZUFIUGUEUFIZJUIUGUDUEUFKUHUJUFUCABLMUKUGUENOPUCABQUCUABQRS $.
      $([19-May-98]$)

    $( Union of complementary parts into whole. $)
    undif3 $p |- ( A (_ B <-> B = ( A u. ( B \ A ) ) ) $=
      ( wss cun wceq cdif ssequn1 undif1 cleq2i bitr ) ABCBABDZEBABAFDZEABGKLBA
      BHIJ $.
      $([22-Mar-98]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    $( Commutative law for intersection of classes.  Exercise 7 of Takeuti and
       Zaring, p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cv wcel wa cab cin ancom biab dfin2 3eqtr4 ) CDZAEZMBEZFZCGONFZCGABH
      BAHPQCNOIJCABKCBAKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( x e. A <-> ( x e. B /\ x e. C ) ) $.
    $( Inference from membership to union. $)
    ineqri   $p |- A = ( B i^i C ) $=
      ( cv wcel wa cab cin cleqabri dfin2 eqtr4 ) BAFZCGNDGHZAICDJOABEKACDLM $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cv wcel wa cab cin eleq2 birand biabdv dfin2 3eqtr4g ) ABEZDFZA
      GZQCGZHZDIQBGZSHZDIACJBCJPTUBDPRUASABQKLMDACNDBCNO $.
      $([14-Dec-93]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
    ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $([26-Dec-93]$)

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i   $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEDABCGH $.
      $([26-Dec-93]$)

    $( Equality inference for intersection of two classes. $)
    ineq2i   $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEDABCGH $.
      $([26-Dec-93]$)
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d   $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $([10-Apr-94]$)

    $( Equality deduction for intersection of two classes. $)
    ineq2d   $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $([10-Apr-94]$)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq12 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
    ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $([8-May-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of Suppes, p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cin wcel cvv wa elisset adantl cv wceq eleq1 bi2and dfin2 vtoclabg
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARIUASTACIJDKZBFZUCCFZHUBDARGUCALUDTUEUAU
      CABMUCACMNDBCOPQ $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Absorption law for intersection of classes.  Theorem 15 of Suppes,
       p. 26. $)
    inabs $p |- A = ( A i^i A ) $=
      ( vx cv wcel anabs ineqri ) BAAABCADEF $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for intersection of classes.  Exercise 9 of Takeuti and
       Zaring, p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa elin biran anass bitr bilan 3bitr4 ineqri ) DABEZCEZA
      BCEZDFZPGZSCGZHZSAGZSBGZUAHZHZSQGUCSRGZHUBUCUDHZUAHUFTUHUASABIJUCUDUAKLSP
      CIUGUEUCSBCIMNO $.
      $([3-May-94]$)
  $}


  $( A rearrangement of intersection. $)
  in12 $p |- ( A i^i ( B i^i C ) ) = ( B i^i ( A i^i C ) ) $=
    ( cin incom ineq1i inass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $([21-Apr-01]$)

  $( A rearrangement of intersection. $)
  in23 $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i B ) $=
    ( cin incom ineq2i inass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $([21-Apr-01]$)

  $( Rearrangement of intersection of 4 classes. $)
  in4 $p |- ( ( A i^i B ) i^i ( C i^i D ) ) =
            ( ( A i^i C ) i^i ( B i^i D ) ) $=
    ( cin in12 ineq2i inass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $([21-Apr-01]$)

  $( Intersection distributes over itself. $)
  indist $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
    ( cin inabs ineq1i in4 eqtr ) ABCDZDAADZIDABDACDDAJIAEFAABCGH $.
    $([6-May-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( A relationship between subclass and intersection.  Exercise 9 of
       Takeuti and Zaring, p. 18. $)
    sseqin1 $p |- ( A (_ B <-> A = ( A i^i B ) ) $=
      ( vx wss cv wcel wa cab wceq cin wi wal wb pm4.71 bial df-ss cleqab
      3bitr4 dfin2 cleq2i bitr4 ) ABDZACEZAFZUCBFZGZCHZIZAABJZIUDUEKZCLUDUFMZCL
      UBUHUJUKCUDUENOCABPUFCAQRUIUGACABSTUA $.
      $([5-Aug-93]$)
  $}

  $( A relationship between subclass and intersection. $)
  sseqin2 $p |- ( A (_ B <-> A = ( B i^i A ) ) $=
    ( wss cin wceq sseqin1 incom cleq2i bitr ) ABCAABDZEABADZEABFJKAABGHI $.
    $([17-May-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of Takeuti and Zaring, p. 18. $)
    inss1 $p |- ( A i^i B ) (_ A $=
      ( vx cin cv wcel elin pm3.26bd ssriv ) CABDZACEZJFKAFKBFKABGHI $.
      $([27-Apr-94]$)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of Takeuti and Zaring, p. 18. $)
  inss2 $p |- ( A i^i B ) (_ B $=
    ( cin wss inss1 incom sseq1i mpbi ) BACZBDABCZBDBAEIJBBAFGH $.
    $([27-Apr-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of Monk1, p. 26. $)
    ssin $p |- ( A (_ ( B i^i C ) <-> ( A (_ B /\ A (_ C ) ) $=
      ( cin wss wa inss1 sstr mpi inss2 jca wceq ineq12 inabs indist 3eqtr4g
      sseqin1 bi2an 3imtr4 impbi ) ABCDZEZABEZACEZFZUBUCUDUBUABEUCBCGAUABHIUBUA
      CEUDBCJAUACHIKAABDZLZAACDZLZFZAAUADZLUEUBUJAADUFUHDAUKAUFAUHMANABCOPUCUGU
      DUIABQACQRAUAQST $.
      $([8-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation. $)
    ssrin $p |- ( A (_ B -> ( A i^i C ) (_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal cin wss wa id imrand elin 3imtr4g 19.20i df-ss
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $([16-Aug-94]$)

    $( Add left intersection to subclass relation. $)
    sslin $p |- ( A (_ B -> ( C i^i A ) (_ ( C i^i B ) ) $=
      ( wss cin ssrin incom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $([19-Oct-99]$)
  $}

  $( Intersection of subclasses. $)
  ss2in $p |- ( ( A (_ B /\ C (_ D ) -> ( A i^i C ) (_ ( B i^i D ) ) $=
    ( cin wss sstr imp ssrin sslin syl2an ) ACEZBCEZFZMBDEZFZLOFZABFCDFNPQLMOGH
    ABCICDBJK $.
    $([5-May-00]$)

  $( Intersection preserves subclass relationship. $)
  ssinss1 $p |- ( A (_ C -> ( A i^i B ) (_ C ) $=
    ( cin wss wi inss1 sstr ax-mp ) ABDZAEACEJCEFABGJACHI $.
    $([14-Sep-99]$)

  ${
    $d x A $.  $d x B $.
    $( Subclass in terms of difference. $)
    ssdifb $p |- ( A (_ B <-> A = ( B \ ( B \ A ) ) ) $=
      ( vx wss cin wceq cdif sseqin2 cv wcel wa wn wi abai iman bilan bitr
      elin eldif bineg 3bitr4 cleqri cleq2i ) ABDABAEZFABBAGZGZFABHUDUFACUDUFCI
      ZBJZUGAJZKZUHUHUILKZLZKZUGUDJUGUFJZUJUHUHUIMZKUMUHUINUOULUHUHUIOPQUGBARUN
      UHUGUEJZLZKUMUGBUESUQULUHUPUKUGBASTPQUAUBUCQ $.
      $([22-Mar-98]$)

    $( Difference with intersection. $)
    difin $p |- ( A \ B ) = ( A \ ( A i^i B ) ) $=
      ( vx cdif cin cv wcel wn wa wi abai imnan bilan bitr eldif elin bineg
      3bitr4 cleqri ) CABDZAABEZDZCFZAGZUCBGZHZIZUDUDUEIZHZIZUCTGUCUBGZUGUDUDUF
      JZIUJUDUFKULUIUDUDUELMNUCABOUKUDUCUAGZHZIUJUCAUAOUNUIUDUMUHUCABPQMNRS $.
      $([31-Mar-98]$)
  $}

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    zfauscl.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation.  Compare
       Exercise 4 of Takeuti and Zaring, p. 22. $)
    zfauscl   $p |- ( A i^i B ) e. V $=
      ( vx vy cin cv wceq wex wel wcel wa wb wal zfaus dfcleq elin bilbi bial
      bitr biex mpbir issetri ) DABFZDGZUDHZDIEDJZEGZAKUHBKZLZMZENZDIUIEDACOUFU
      LDUFUGUHUDKZMZENULEUEUDPUNUKEUMUJUGUHABQRSTUAUBUC $.
      $([5-Aug-93]$)
  $}

  ${
    zfauscl2.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation. $)
    zfauscl2   $p |- ( B i^i A ) e. V $=
      ( cin cvv incom zfauscl eqeltr ) BADABDEBAFABCGH $.
      $([27-Apr-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Closed-form, generalized Separation Scheme. $)
    zfausclg $p |- ( A e. C -> ( A i^i B ) e. V ) $=
      ( vx cv cin cvv wcel wceq ineq1 eleq1d visset zfauscl vtoclg ) DEZBFZGHAB
      FZGHDACOAIPQGOABJKOBDLMN $.
      $([7-Apr-95]$)
  $}

  ${
    ssex.1 $e |- B e. V $.
    $( The subset of a set is also a set.  Exercise 3 of Takeuti and Zaring,
       p. 22. $)
    ssex  $p |- ( A (_ B -> A e. V ) $=
      ( wss cin wceq cvv wcel sseqin1 zfauscl2 eleq1 mpbiri sylbi ) ABDAABEZFZA
      GHZABIOPNGHBACJANGKLM $.
      $([27-Apr-94]$)
  $}

  ${
    ssexi.1 $e |- B e. V $.
    ssexi.2 $e |- A (_ B $.
    $( The subset of a set is also a set. $)
    ssexi   $p |- A e. V $=
      ( wss cvv wcel ssex ax-mp ) ABEAFGDABCHI $.
      $([9-Sep-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The subset of a set is also a set.  Exercise 3 of Takeuti and Zaring,
       p. 22 (generalized). $)
    ssexg  $p |- ( B e. C -> ( A (_ B -> A e. V ) ) $=
      ( vx cv wss cvv wcel wi wceq sseq2 birimd visset ssex vtoclg ) ADEZFZAGHZ
      IABFZRIDBCPBJQSRPBAKLAPDMNO $.
      $([14-Aug-94]$)
  $}

  $( Existence of a difference. $)
  difexg $p |- ( A e. C -> ( A \ B ) e. V ) $=
    ( wcel cdif wss cvv difss ssexg mpi ) ACDABEZAFKGDABHKACIJ $.
    $([26-May-98]$)

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    zfausab.1 $e |- A e. V $.
    $( Separation Scheme in terms of an abstraction class. $)
    zfausab   $p |- { x | ( x e. A /\ ph ) } e. V $=
      ( cv wcel wa cab ssab ssexi ) BECFAGBHCDABCIJ $.
      $([8-Jun-94]$)
  $}

  ${
    $d x A $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    zfausrabg $p |- ( A e. B -> { x e. A | ph } e. V ) $=
      ( wcel crab wss cvv ssrab ssexg mpi ) CDEABCFZCGLHEABCILCDJK $.
      $([23-Oct-99]$)
  $}

  ${
    $d x A $.
    zfausrab.1 $e |- A e. V $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    zfausrab   $p |- { x e. A | ph } e. V $=
      ( cvv wcel crab zfausrabg ax-mp ) CEFABCGEFDABCEHI $.
      $([19-Jul-96]$)
  $}

