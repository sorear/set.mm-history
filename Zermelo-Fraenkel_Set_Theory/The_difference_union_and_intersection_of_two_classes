$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for class difference. $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 birand biabdv df-dif 3eqtr4g ) ABE
      ZDFZAGZRCGHZIZDJRBGZTIZDJACKBCKQUAUCDQSUBTABRLMNDACODBCOP $.
      $( [10-Feb-97] $)

    $( Equality theorem for class difference. $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 binegd biland biabdv df-dif
      3eqtr4g ) ABEZDFZCGZSAGZHZIZDJTSBGZHZIZDJCAKCBKRUCUFDRUBUETRUAUDABSLMNODC
      APDCBPQ $.
      $( [10-Feb-97] $)
  $}

  ${
    difeq1i.1 $e |- A = B $.
    $( Inference adding difference to the right in a class equality. $)
    difeq1i $p |- ( A \ C ) = ( B \ C ) $=
      ( wceq cdif difeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [15-Nov-02] $)

    $( Inference adding difference to the left in a class equality. $)
    difeq2i $p |- ( C \ A ) = ( C \ B ) $=
      ( wceq cdif difeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [15-Nov-02] $)

    ${
      difeq12i.2 $e |- C = D $.
      $( Equality inference for class difference. $)
      difeq12i $p |- ( A \ C ) = ( B \ D ) $=
        ( cdif difeq1i difeq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
        $( [11-Sep-04] $) $( [29-Aug-04] $)
    $}
  $}

  ${
    difeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding difference to the right in a class equality. $)
    difeq1d $p |- ( ph -> ( A \ C ) = ( B \ C ) ) $=
      ( wceq cdif difeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [15-Nov-02] $)

    $( Deduction adding difference to the left in a class equality. $)
    difeq2d $p |- ( ph -> ( C \ A ) = ( C \ B ) ) $=
      ( wceq cdif difeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [15-Nov-02] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    difeqri.1 $e |- ( ( x e. A /\ -. x e. B ) <-> x e. C ) $.
    $( Inference from membership to difference. $)
    difeqri $p |- ( A \ B ) = C $=
      ( cdif cv wcel wn wa cab df-dif bicom biabri eqtr4 ) BCFAGZBHPCHIJZAKDABC
      LQADQPDHEMNO $.
      $( [17-May-98] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbdif.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbdif.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for class difference. $)
    hbdif $p |- ( y e. ( A \ B ) -> A. x y e. ( A \ B ) ) $=
      ( cv wcel wn wa wal cdif hbne hban eldif bial 3imtr4 ) BGZCHZRDHZIZJZUBAK
      RCDLHZUCAKSUAAETAFMNRCDOZUCUBAUDPQ $.
      $( [3-Dec-03] $) $( [3-Dec-03] $)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
    ( cdif wcel wn eldif pm3.26bd ) ABCDEABEACEFABCGH $.
    $( [29-Apr-94] $)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
    ( cdif wcel wn eldif pm3.27bd ) ABCDEABEACEFABCGH $.
    $( [3-May-94] $)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
    ( cdif wcel eldifn con2i ) ACBDEABEACBFG $.
    $( [27-Jun-94] $)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
    ( wcel cdif wn wa eldif biimpr exp con1d imp ) ABDZABCEDZFACDZMONMOFZNNMPGA
    BCHIJKL $.
    $( [28-Jun-94] $)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Double class difference.  Exercise 11 of [TakeutiZaring] p. 22. $)
    difdif $p |- ( A \ ( B \ A ) ) = A $=
      ( vx cdif cv wcel wn wa wi eldif bineg iman bitr4 bilan pm4.45im difeqri
      ) CABADZACEZAFZRQFZGZHSRBFZSIZHSUAUCSUAUBSGHZGUCTUDRBAJKUBSLMNSUBOMP $.
      $( [17-May-98] $)

    $( Subclass relationship for class difference.  Exercise 14 of [TakeutiZaring]
       p. 22. $)
    difss $p |- ( A \ B ) (_ A $=
      ( vx cdif cv eldifi ssriv ) CABDACEABFG $.
      $( [29-Apr-94] $)
  $}

  ${
    $d x A $.
    $( Double complement under universal class.  Exercise 4.10(s) of
       [Mendelson] p. 231. $)
    ddif $p |- ( V \ ( V \ A ) ) = A $=
      ( vx cvv cdif cv wcel wn wa eldif visset mpbiran bicon2 biantrur bitr2
      difeqri ) BCCADZABEZAFZQPFZGZQCFZTHSRSUARGQCAIBJZKLUATUBMNO $.
      $( [8-Jan-02] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Contraposition law for subsets. $)
    ssconb $p |- ( ( A (_ C /\ B (_ C ) ->
                  ( A (_ ( C \ B ) <-> B (_ ( C \ A ) ) ) $=
      ( vx wss wa cv wcel cdif wi wal wn wb pm5.1 ssel syl2an bi2.03 a1i
      bi2and jcab 3bitr4g eldif bilim bialdv dfss2 ) ACEZBCEZFZDGZAHZUICBIZHZJZ
      DKUIBHZUICAIZHZJZDKAUKEBUOEUHUMUQDUHUJUICHZUNLZFZJZUNURUJLZFZJZUMUQUHUJUR
      JZUJUSJZFUNURJZUNVBJZFVAVDUHVEVGVFVHVEVGVEVGMUFUGVEVGNACUIOBCUIOPVFVHMUHU
      JUNQRSUJURUSTUNURVBTUAULUTUJUICBUBUCUPVCUNUICAUBUCUAUDDAUKUEDBUOUEUA $.
      $( [22-Mar-98] $)

    $( Contraposition law for subsets.  Exercise 15 of [TakeutiZaring]
       p. 22. $)
    sscon $p |- ( A (_ B -> ( C \ B ) (_ ( C \ A ) ) $=
      ( vx wss cdif cv wcel wn wa ssel con3d imland eldif 3imtr4g ssrdv ) ABEZD
      CBFZCAFZQDGZCHZTBHZIZJUATAHZIZJTRHTSHQUCUEUAQUDUBABTKLMTCBNTCANOP $.
      $( [22-Mar-98] $)

    $( Difference law for subsets. $)
    ssdif $p |- ( A (_ B -> ( A \ C ) (_ ( B \ C ) ) $=
      ( vx wss cdif cv wcel wn wa ssel imrand eldif 3imtr4g ssrdv ) ABEZDACFZBC
      FZPDGZAHZSCHIZJSBHZUAJSQHSRHPTUBUAABSKLSACMSBCMNO $.
      $( [28-May-98] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in class union.  Theorem 12 of [Suppes] p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cun wcel cvv wo elisset jaoi cv wceq eleq1 bi2ord df-un elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARITSUAABIACIJDKZBFZUCCFZHUBDARGUCALUDTUE
      UAUCABMUCACMNDBCOPQ $.
      $( [7-Aug-94] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( ( x e. A \/ x e. B ) <-> x e. C ) $.
    $( Inference from membership to union. $)
    uneqri $p |- ( A u. B ) = C $=
      ( cun cv wcel wo elun bitr cleqri ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for union of classes.  Theorem 23 of [Suppes] p. 27. $)
    unidm $p |- ( A u. A ) = A $=
      ( vx cv wcel oridm uneqri ) BAAABCADEF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Commutative law for union of classes.  Exercise 6 of [TakeutiZaring]
       p. 17. $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cun cv wcel wo orcom elun 3bitr4 cleqri ) CABDZBADZCEZAFZNBFZGPOGNLF
      NMFOPHNABINBAIJK $.
      $( [25-Jun-98] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cun cv wcel wo eleq2 birord elun 3bitr4g cleqrd ) ABEZDACFZBCFZ
      ODGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [5-Aug-93] $)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
    ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [5-Aug-93] $)

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i $p |- ( A u. C ) = ( B u. C ) $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [30-Aug-93] $)

    $( Inference adding union to the left in a class equality. $)
    uneq2i $p |- ( C u. A ) = ( C u. B ) $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [30-Aug-93] $)

    ${
      uneq12i.2 $e |- C = D $.
      $( Equality inference for union of two classes. $)
      uneq12i $p |- ( A u. C ) = ( B u. D ) $=
        ( cun uneq1i uneq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
        $( [18-Aug-04] $) $( [12-Aug-04] $)
    $}
  $}

  ${
    uneq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the right in a class equality. $)
    uneq1d $p |- ( ph -> ( A u. C ) = ( B u. C ) ) $=
      ( wceq cun uneq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [29-Mar-98] $)

    $( Deduction adding union to the left in a class equality. $)
    uneq2d $p |- ( ph -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [29-Mar-98] $)
  $}

  $( Equality theorem for union of two classes. $)
  uneq12 $p |- ( ( A = B /\ C = D ) -> ( A u. C ) = ( B u. D ) ) $=
    ( wceq cun uneq1 uneq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [29-Mar-98] $)

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbun.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbun.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the union of classes. $)
    hbun $p |- ( y e. ( A u. B ) -> A. x y e. ( A u. B ) ) $=
      ( cv wcel wo wal cun hbor elun bial 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRAE
      FLPCDMZTSAUANO $.
      $( [15-Sep-03] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for union of classes.  Exercise 8 of [TakeutiZaring]
       p. 17. $)
    unass $p |- ( ( A u. B ) u. C ) = ( A u. ( B u. C ) ) $=
      ( vx cun cv wcel wo orass elun biror bilor 3bitr4 bitr4 uneqri ) DABEZCAB
      CEZEZDFZPGZSCGZHZSAGZSQGZHZSRGUCSBGZHZUAHUCUFUAHZHUBUEUCUFUAITUGUASABJKUD
      UHUCSBCJLMSAQJNO $.
      $( [3-May-94] $)
  $}

  $( A rearrangement of union. $)
  un12 $p |- ( A u. ( B u. C ) ) = ( B u. ( A u. C ) ) $=
    ( cun uncom uneq1i unass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $( [26-Aug-04] $) $( [12-Aug-04] $)

  $( A rearrangement of union. $)
  un23 $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. B ) $=
    ( cun uncom uneq2i unass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $( [29-Aug-04] $) $( [12-Aug-04] $)

  $( A rearrangement of the union of 4 classes. $)
  un4 $p |- ( ( A u. B ) u. ( C u. D ) ) =
            ( ( A u. C ) u. ( B u. D ) ) $=
    ( cun un12 uneq2i unass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $( [30-Aug-04] $) $( [12-Aug-04] $)

  $( Union distributes over itself. $)
  unundi $p |- ( A u. ( B u. C ) ) = ( ( A u. B ) u. ( A u. C ) ) $=
    ( cun unidm uneq1i un4 eqtr3 ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [15-Sep-04] $) $( [17-Aug-04] $)

  $( Union distributes over itself. $)
  unundir $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. ( B u. C ) ) $=
    ( cun unidm uneq2i un4 eqtr3 ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [9-Sep-04] $) $( [17-Aug-04] $)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass relationship for union of classes.  Theorem 25 of [Suppes]
       p. 27. $)
    ssun1 $p |- A (_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssriv ) CAABDZCEZAFZNMBFZGMLFNOHMABIJ
      K $.
      $( [5-Aug-93] $)
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A (_ ( B u. A ) $=
    ( cun ssun1 uncom sseqtr ) AABCBACABDABEF $.
    $( [30-Aug-93] $)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A (_ B -> A (_ ( B u. C ) ) $=
    ( wss cun ssun1 sstr mpi ) ABDBBCEZDAIDBCFABIGH $.
    $( [5-Aug-93] $)

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A (_ B -> A (_ ( C u. B ) ) $=
    ( wss cun ssun2 sstr mpi ) ABDBCBEZDAIDBCFABIGH $.
    $( [14-Aug-94] $)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
    ( cun ssun1 sseli ) BBCDABCEF $.
    $( [5-Aug-93] $)

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
    ( cun ssun2 sseli ) BCBDABCEF $.
    $( [30-Aug-93] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Subclass law for union of classes. $)
    unss1 $p |- ( A (_ B -> ( A u. C ) (_ ( B u. C ) ) $=
      ( vx cv wcel wi wal cun wss wo id imrord elun 3imtr4g 19.20i dfss2
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $( [14-Oct-99] $)

    $( A relationship between subclass and union.  Theorem 26 of [Suppes]
       p. 27. $)
    ssequn1 $p |- ( A (_ B <-> ( A u. B ) = B ) $=
      ( vx cun wceq cv wcel wo cab wss df-un cleq2i cleqcom wi wal wb pm4.72
      bial dfss2 cleqab 3bitr4 3bitr4r ) BABDZEBCFZAGZUDBGZHZCIZEZUCBEABJZUCUHB
      CABKLUCBMUEUFNZCOUFUGPZCOUJUIUKULCUEUFQRCABSUGCBTUAUB $.
      $( [29-Aug-04] $) $( [30-Aug-93] $)
  $}

  $( Subclass law for union of classes.  Exercise 7 of [TakeutiZaring]
     p. 18. $)
  unss2 $p |- ( A (_ B -> ( C u. A ) (_ ( C u. B ) ) $=
    ( wss cun unss1 uncom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [14-Oct-99] $)

  $( Subclass law for union of classes. $)
  unss12 $p |- ( ( A (_ B /\ C (_ D ) -> ( A u. C ) (_ ( B u. D ) ) $=
    ( wss wa cun unss1 adantr unss2 adantl sstrd ) ABEZCDEZFACGZBCGZBDGZMOPENAB
    CHINPQEMCDBJKL $.
    $( [7-Jun-04] $) $( [2-Jun-04] $)

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A (_ B <-> ( B u. A ) = B ) $=
    ( wss cun wceq ssequn1 uncom cleq1i bitr ) ABCABDZBEBADZBEABFJKBABGHI $.
    $( [23-Sep-04] $) $( [13-Jun-94] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( The union of two subclasses is a subclass.  Theorem 27 of [Suppes] p. 27
       and its converse. $)
    unss $p |- ( ( A (_ C /\ B (_ C ) <-> ( A u. B ) (_ C ) $=
      ( vx cv cun wcel wi wal wa wss wo elun birim jaob bitr bial dfss2 bi2an
      19.26 bitr4 3bitr4r ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZUDCKACKZBC
      KZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUMUNURUOUSDA
      CRDBCRSUIUKDTUAUB $.
      $( [15-Jun-04] $) $( [11-Jun-04] $)
  $}

  ${
    unssi.1 $e |- A (_ C $.
    unssi.2 $e |- B (_ C $.
    $( An inference that the union of two subclasses is a subclass.
       Contributed by Raph Levien, 10-Dec-02. $)
    unssi $p |- ( A u. B ) (_ C $=
      ( wss wa cun pm3.2i unss mpbi ) ACFZBCFZGABHCFLMDEIABCJK $.
      $( [14-May-03] $)
  $}

  $( A condition that implies inclusion in the union of two classes. $)
  ssun $p |- ( ( A (_ B \/ A (_ C ) -> A (_ ( B u. C ) ) $=
    ( wss cun ssun3 ssun4 jaoi ) ABDABCEDACDABCFACBGH $.
    $( [23-Nov-03] $) $( [23-Nov-03] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of [Suppes] p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cin wcel cvv wa elisset adantl cv wceq eleq1 bi2and df-in elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARIUASTACIJDKZBFZUCCFZHUBDARGUCALUDTUEUAU
      CABMUCACMNDBCOPQ $.
      $( [29-Apr-94] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Commutative law for intersection of classes.  Exercise 7 of
       [TakeutiZaring] p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cin cv wcel wa ancom elin 3bitr4 cleqri ) CABDZBADZCEZAFZNBFZGPOGNLF
      NMFOPHNABINBAIJK $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( ( x e. A /\ x e. B ) <-> x e. C ) $.
    $( Inference from membership to intersection. $)
    ineqri $p |- ( A i^i B ) = C $=
      ( cin cv wcel wa elin bitr cleqri ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cin cv wcel wa eleq2 birand elin 3bitr4g cleqrd ) ABEZDACFZBCFZ
      ODGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [14-Dec-93] $)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
    ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [26-Dec-93] $)

  $( Equality theorem for intersection of two classes. $)
  ineq12 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
    ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [8-May-94] $)

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [26-Dec-93] $)

    $( Equality inference for intersection of two classes. $)
    ineq2i $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [26-Dec-93] $)

    ${
      ineq12i.2 $e |- C = D $.
      $( Equality inference for intersection of two classes. $)
      ineq12i $p |- ( A i^i C ) = ( B i^i D ) $=
        ( cin ineq1i ineq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
        $( [24-Jun-04] $) $( [24-Jun-04] $)
    $}
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [10-Apr-94] $)

    $( Equality deduction for intersection of two classes. $)
    ineq2d $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [10-Apr-94] $)

    ${
      ineq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for intersection of two classes. $)
      ineq12d $p |- ( ph -> ( A i^i C ) = ( B i^i D ) ) $=
        ( cin ineq1d ineq2d eqtrd ) ABDHCDHCEHABCDFIADECGJK $.
        $( [25-Jun-04] $) $( [24-Jun-04] $)
    $}
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbin.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbin.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the intersection of classes. $)
    hbin $p |- ( y e. ( A i^i B ) -> A. x y e. ( A i^i B ) ) $=
      ( cv wcel wa wal cin hban elin bial 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRAE
      FLPCDMZTSAUANO $.
      $( [15-Sep-03] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    birabrdv.1 $e |- ( ph -> ( x e. B -> ( x e. A <-> ch ) ) ) $.
    $( Deduction from wff to restricted abstraction class. $)
    birabrdv $p |- ( ph -> ( B i^i A ) = { x e. B | ch } ) $=
      ( cin cv wcel wa cab crab pm5.32d elin syl5bb biabrdv df-rab cleqcomi
      syl6eq ) AEDGZCHZEIZBJZCKZBCELZAUCCTAUBUADIZJUCUATIAUBUFBFMUAEDNOPUEUDBCE
      QRS $.
      $( [16-Sep-04] $) $( [27-Aug-04] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Union of two class abstractions. $)
    unab $p |- ( { x | ph } u. { x | ps } ) = { x | ( ph \/ ps ) } $=
      ( vy cab cun wo cv wcel wsb df-clab bi2or sbor bitr4 elun 3bitr4 cleqri
      ) DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACDJZUCTIUCUBIUFACDJZBCDJZGUGUDUHUEUIAD
      CKBDCKLABCDMNUCRSOUADCKPQ $.
      $( [29-Sep-02] $)

    $( Intersection of two class abstractions. $)
    inab $p |- ( { x | ph } i^i { x | ps } ) = { x | ( ph /\ ps ) } $=
      ( vy cab cin wa cv wcel wsb df-clab bi2an sban bitr4 elin 3bitr4 cleqri
      ) DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACDJZUCTIUCUBIUFACDJZBCDJZGUGUDUHUEUIAD
      CKBDCKLABCDMNUCRSOUADCKPQ $.
      $( [29-Sep-02] $)
  $}

  $( Union of two restricted class abstractions. $)
  unrab $p |- ( { x e. A | ph } u. { x e. A | ps } ) =
              { x e. A | ( ph \/ ps ) } $=
    ( cv wcel wa cab cun wo crab unab andi biabi cleqcomi eqtr df-rab uneq12i
    3eqtr4 ) CEDFZAGZCHZTBGZCHZIZTABJZGZCHZACDKZBCDKZIUFCDKUEUAUCJZCHZUHUAUCCLU
    HULUGUKCTABMNOPUIUBUJUDACDQBCDQRUFCDQS $.
    $( [26-Mar-04] $) $( [25-Mar-04] $)

  ${
    $d x A $.
    $( Alternate definition of restricted abstraction class. $)
    dfrab2 $p |- { x e. A | ph } = ( { x | ph } i^i A ) $=
      ( crab cv wcel wa cab cin df-rab inab abid2 ineq1i eqtr3 incom 3eqtr ) AB
      CDBECFZAGBHZCABHZIZSCIABCJQBHZSIRTQABKUACSBCLMNCSOP $.
      $( [20-Sep-03] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for intersection of classes.  Theorem 15 of [Suppes]
       p. 26. $)
    inidm $p |- ( A i^i A ) = A $=
      ( vx cv wcel anidm ineqri ) BAAABCADEF $.
      $( [5-Aug-93] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $( ` x ` is dummy. $)
    $( Associative law for intersection of classes.  Exercise 9 of [TakeutiZaring]
       p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa anass elin bilan bitr4 biran 3bitr4 ineqri ) DABEZCAB
      CEZEZDFZAGZSBGZHZSCGZHZTSQGZHZSPGZUCHSRGUDTUAUCHZHUFTUAUCIUEUHTSBCJKLUGUB
      UCSABJMSAQJNO $.
      $( [3-May-94] $)
  $}

  $( A rearrangement of intersection. $)
  in12 $p |- ( A i^i ( B i^i C ) ) = ( B i^i ( A i^i C ) ) $=
    ( cin incom ineq1i inass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $( [21-Apr-01] $)

  $( A rearrangement of intersection. $)
  in23 $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i B ) $=
    ( cin incom ineq2i inass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $( [21-Apr-01] $)

  $( Rearrangement of intersection of 4 classes. $)
  in4 $p |- ( ( A i^i B ) i^i ( C i^i D ) ) =
            ( ( A i^i C ) i^i ( B i^i D ) ) $=
    ( cin in12 ineq2i inass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $( [21-Apr-01] $)

  $( Intersection distributes over itself. $)
  inindi $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
    ( cin inidm ineq1i in4 eqtr3 ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [6-May-94] $)

  $( Intersection distributes over itself. $)
  inindir $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i ( B i^i C ) ) $=
    ( cin inidm ineq2i in4 eqtr3 ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [18-Sep-04] $) $( [17-Aug-04] $)

  $( A relationship between subclass and intersection.  Similar to
     Exercise 9 of [TakeutiZaring] p. 18. $)
  sseqin2 $p |- ( A (_ B <-> ( B i^i A ) = A ) $=
    ( wss cin wceq df-ss incom cleq1i bitr ) ABCABDZAEBADZAEABFJKAABGHI $.
    $( [23-Sep-04] $) $( [17-May-94] $)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of [TakeutiZaring] p. 18. $)
    inss1 $p |- ( A i^i B ) (_ A $=
      ( vx cin cv wcel elin pm3.26bd ssriv ) CABDZACEZJFKAFKBFKABGHI $.
      $( [27-Apr-94] $)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of [TakeutiZaring] p. 18. $)
  inss2 $p |- ( A i^i B ) (_ B $=
    ( cin incom inss1 eqsstr3 ) ABCBACBBADBAEF $.
    $( [27-Apr-94] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of [Monk1] p. 26. $)
    ssin $p |- ( ( A (_ B /\ A (_ C ) <-> A (_ ( B i^i C ) ) $=
      ( wss wa cin wceq ineq12 inindi syl5eq inidm syl6eq df-ss bi2an 3imtr4
      inss1 sstr mpi inss2 jca impbi ) ABDZACDZEZABCFZDZABFZAGZACFZAGZEZAUEFZAG
      UDUFUKULAAFZAUKUGUIFUMULUGAUIAHABCIJAKLUBUHUCUJABMACMNAUEMOUFUBUCUFUEBDUB
      BCPAUEBQRUFUECDUCBCSAUECQRTUA $.
      $( [16-Jun-04] $) $( [15-Jun-04] $)
  $}

  ${
    ssini.1 $e |- A (_ B $.
    ssini.2 $e |- A (_ C $.
    $( An inference showing that the a subclass of two classes is a subclass of
       their intersection. $)
    ssini $p |- A (_ ( B i^i C ) $=
      ( wss wa cin pm3.2i ssin mpbi ) ABFZACFZGABCHFLMDEIABCJK $.
      $( [24-Nov-03] $) $( [24-Nov-03] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation. $)
    ssrin $p |- ( A (_ B -> ( A i^i C ) (_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal cin wss wa id imrand elin 3imtr4g 19.20i dfss2
      3imtr4 ) DEZAFZSBFZGZDHSACIZFZSBCIZFZGZDHABJUCUEJUBUGDUBTSCFZKUAUHKUDUFUB
      TUAUHUBLMSACNSBCNOPDABQDUCUEQR $.
      $( [16-Aug-94] $)

    $( Add left intersection to subclass relation. $)
    sslin $p |- ( A (_ B -> ( C i^i A ) (_ ( C i^i B ) ) $=
      ( wss cin ssrin incom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $( [19-Oct-99] $)
  $}

  $( Intersection of subclasses. $)
  ss2in $p |- ( ( A (_ B /\ C (_ D ) -> ( A i^i C ) (_ ( B i^i D ) ) $=
    ( wss cin ssrin sslin sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [5-May-00] $)

  $( Intersection preserves subclass relationship. $)
  ssinss1 $p |- ( A (_ C -> ( A i^i B ) (_ C ) $=
    ( cin wss wi inss1 sstr ax-mp ) ABDZAEACEJCEFABGJACHI $.
    $( [14-Sep-99] $)

  $( Negation of subclass expressed in terms of intersection and proper
     subclass. $)
  nssinpss $p |- ( -. A (_ B <-> ( A i^i B ) (. A ) $=
    ( cin wss wn wa wpss inss1 biantrur ssid ssin bitr bineg dfpss3 3bitr4 ) AA
    BCZDZEZPADZRFABDZEPAGSRABHITQTAADZTFQUATAJIAABKLMPANO $.
    $( [1-Jul-04] $) $( [30-Jun-04] $)

  $( Negation of subclass expressed in terms of proper subclass and union. $)
  nsspssun $p |- ( -. A (_ B <-> B (. ( A u. B ) ) $=
    ( cun wss wn wa wpss ssun2 biantrur ssid biantru unss bitr bineg dfpss3
    3bitr4 ) ABCZBDZEZBQDZSFABDZEBQGTSBAHIUARUAUABBDZFRUBUABJKABBLMNBQOP $.
    $( [18-Sep-04] $) $( [15-Sep-04] $)

  ${
    $d x A $.  $d x B $.
    $( Subclass defined in terms of class difference.  See comments under
       ~ dfun2 . $)
    dfss4 $p |- ( A (_ B <-> ( B \ ( B \ A ) ) = A ) $=
      ( vx wss cin wceq cdif sseqin2 cv wcel wa wn wi abai iman bilan bitr
      elin eldif bineg 3bitr4 cleqri cleq1i ) ABDBAEZAFBBAGZGZAFABHUDUFACUDUFCI
      ZBJZUGAJZKZUHUHUILKZLZKZUGUDJUGUFJZUJUHUHUIMZKUMUHUINUOULUHUHUIOPQUGBARUN
      UHUGUEJZLZKUMUGBUESUQULUHUPUKUGBASTPQUAUBUCQ $.
      $( [23-Sep-04] $) $( [22-Mar-98] $)

    $( An alternate definition of the union of two classes in terms of class
       difference, requiring no dummy variables.  Along with ~ dfin2 and
       ~ dfss4 it shows we can express union, intersection, and subset directly
       in terms of the single "primitive" operation ` \ `  (class
       difference). $)
    dfun2 $p |- ( A u. B ) = ( V \ ( ( V \ A ) \ B ) ) $=
      ( vx cun cvv cdif cv wcel wo wn wa eldif visset mpbiran biran ioran
      3bitr4 bicon2 elun cleqri ) CABDZEEAFZBFZFZCGZAHZUEBHZIZUEUCHZJZUEUAHUEUD
      HZUIUHUEUBHZUGJZKUFJZUMKUIUHJULUNUMULUEEHZUNUEEALCMZNOUEUBBLUFUGPQRUEABSU
      KUOUJUEEUCLUPNQT $.
      $( [12-Jun-04] $) $( [10-Jun-04] $)

    $( An alternate definition of the intersection of two classes in terms of
       class difference, requiring no dummy variables.  See comments under
       ~ dfun2 .  Another version is given by ~ dfin4 . $)
    dfin2 $p |- ( A i^i B ) = ( A \ ( V \ B ) ) $=
      ( vx cin cvv cdif cv wcel wa wn eldif visset mpbiran bicon2 bilan elin
      3bitr4 cleqri ) CABDZAEBFZFZCGZAHZUBBHZIUCUBTHZJZIUBSHUBUAHUDUFUCUEUDUEUB
      EHUDJUBEBKCLMNOUBABPUBATKQR $.
      $( [12-Jun-04] $) $( [10-Jun-04] $)

    $( Difference with intersection.  Theorem 33 of [Suppes] p. 29. $)
    difin $p |- ( A \ ( A i^i B ) ) = ( A \ B ) $=
      ( vx cin cdif cv wcel wn wa wi abai imnan bilan bitr eldif elin bineg
      3bitr4r cleqri ) CAABDZEZABEZCFZAGZUCBGZHZIZUDUDUEIZHZIZUCUBGUCUAGZUGUDUD
      UFJZIUJUDUFKULUIUDUDUELMNUCABOUKUDUCTGZHZIUJUCATOUNUIUDUMUHUCABPQMNRS $.
      $( [18-Aug-04] $) $( [31-Mar-98] $)
  $}

  $( Union defined in terms of intersection (DeMorgan's law).  Definition of
     union in [Mendelson] p. 231. $)
  dfun3 $p |- ( A u. B ) = ( V \ ( ( V \ A ) i^i ( V \ B ) ) ) $=
    ( cun cvv cdif cin dfun2 dfin2 ddif difeq2i eqtr2 eqtr ) ABCDDAEZBEZEDMDBEZ
    FZEABGNPDPMDOEZENMOHQBMBIJKJL $.
    $( [8-Jan-02] $)

  $( Intersection defined in terms of union (DeMorgan's law.  Similar to
     Exercise 4.10(n) of [Mendelson] p. 231. $)
  dfin3 $p |- ( A i^i B ) = ( V \ ( ( V \ A ) u. ( V \ B ) ) ) $=
    ( cvv cdif cun cin ddif dfun2 difeq1i difeq2i eqtr dfin2 3eqtr4r ) CCACBDZD
    ZDZDOCCADZNEZDABFOGRPCRCCQDZNDZDPQNHTOCSANAGIJKJABLM $.
    $( [8-Jan-02] $)

  $( Alternate definition of the union of two classes.  Exercise 4.10(q) of
     [Mendelson] p. 231. $)
  dfin4 $p |- ( A i^i B ) = ( A \ ( A \ B ) ) $=
    ( cin cdif wss wceq inss1 dfss4 mpbi difin difeq2i eqtr3 ) AAABCZDZDZMAABDZ
    DMAEOMFABGMAHINPAABJKL $.
    $( [25-Nov-03] $) $( [25-Nov-03] $)

  $( Intersection with universal complement.  Remark in [Stoll] p. 20. $)
  invdif $p |- ( A i^i ( V \ B ) ) = ( A \ B ) $=
    ( cvv cdif cin dfin2 ddif difeq2i eqtr ) ACBDZEACJDZDABDAJFKBABGHI $.
    $( [18-Aug-04] $) $( [17-Aug-04] $)

  $( Intersection with class difference.  Theorem 34 of [Suppes] p. 29. $)
  indif $p |- ( A i^i ( A \ B ) ) = ( A \ B ) $=
    ( cdif cin dfin4 difeq2i difin eqtr3 eqtr ) AABCZDAAJCZCZJAJEAABDZCLJMKAABE
    FABGHI $.
    $( [3-Sep-04] $) $( [17-Aug-04] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Distributive law for intersection over union.  Exercise 10 of
       [TakeutiZaring] p. 17. $)
    indi $p |- ( A i^i ( B u. C ) ) = ( ( A i^i B ) u. ( A i^i C ) ) $=
      ( vx cun cin cv wcel wa wo andi elun bilan elin bi2or 3bitr4 cleqri ) DAB
      CEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJIZUD
      UKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $( [30-Sep-02] $)

    $( Distributive law for union over intersection.  Exercise 11 of
       [TakeutiZaring] p. 17. $)
    undi $p |- ( A u. ( B i^i C ) ) = ( ( A u. B ) i^i ( A u. C ) ) $=
      ( vx cin cun cv wcel wo wa ordi elin bilor elun bi2an 3bitr4 cleqri ) DAB
      CEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJIZUD
      UKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $( [30-Sep-02] $)
  $}

  $( Distributive law for intersection over union.  Theorem 28 of [Suppes]
     p. 27. $)
  indir $p |- ( ( A u. B ) i^i C ) = ( ( A i^i C ) u. ( B i^i C ) ) $=
    ( cun cin indi incom uneq12i 3eqtr4 ) CABDZECAEZCBEZDJCEACEZBCEZDCABFJCGMKN
    LACGBCGHI $.
    $( [30-Sep-02] $)

  $( Distributive law for union over intersection.  Theorem 29 of [Suppes]
     p. 27. $)
  undir $p |- ( ( A i^i B ) u. C ) = ( ( A u. C ) i^i ( B u. C ) ) $=
    ( cin cun undi uncom wceq ineq12 mp2an 3eqtr4 ) CABDZECAEZCBEZDZLCEACEZBCEZ
    DZCABFLCGPMHQNHROHACGBCGPMQNIJK $.
    $( [30-Sep-02] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Deduce equality from equalities of union and intersection.  Exercise 20
       of [Enderton] p. 32 and its converse. $)
    unineq $p |- ( ( ( A u. C ) = ( B u. C ) /\ ( A i^i C ) = ( B i^i C ) )
                 <-> A = B ) $=
      ( vx cun wceq cin wa cv wcel wb wi iba bi2bid eleq2 elin 3bitr3g syl5bir
      adantld wn wo biorf uncom cleq12i sylbi elun adantrd pm2.61i cleqrd
      uneq1 ineq1 jca impbi ) ACEZBCEZFZACGZBCGZFZHZABFZUTDABDIZCJZUTVBAJZVBBJZ
      KZLVCUSVFUPVCVFVDVCHZVEVCHZKUSVCVDVGVEVHVCVDMVCVEMNUSVBUQJVBURJVGVHUQURVB
      OVBACPVBBCPQRSVCTZUPVFUSVIVFVCVDUAZVCVEUAZKUPVIVDVJVEVKVCVDUBVCVEUBNUPVBC
      AEZJZVBCBEZJZVJVKUPVLVNFVMVOKUNVLUOVNACUCBCUCUDVLVNVBOUEVBCAUFVBCBUFQRUGU
      HUIVAUPUSABCUJABCUKULUM $.
      $( [25-Sep-04] $) $( [10-Aug-04] $)
  $}

  $( Distributive law for class difference.  Theorem 39 of [Suppes] p. 29. $)
  difundi $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) i^i ( A \ C ) ) $=
    ( cun cdif cvv cin dfun3 difeq2i inindi dfin2 invdif ineq12i 3eqtr3 eqtr )
    ABCDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASGAQGZARGZGUAUDAQRJASKUEUBUFUCABLAC
    LMNO $.
    $( [31-Aug-04] $) $( [17-Aug-04] $)

  $( Distributive law for class difference. $)
  difundir $p |- ( ( A u. B ) \ C ) = ( ( A \ C ) u. ( B \ C ) ) $=
    ( cun cvv cdif cin indir invdif uneq12i 3eqtr3 ) ABDZECFZGAMGZBMGZDLCFACFZB
    CFZDABMHLCINPOQACIBCIJK $.
    $( [18-Aug-04] $) $( [17-Aug-04] $)

  $( Distributive law for class difference.  Theorem 40 of [Suppes] p. 29. $)
  difindi $p |- ( A \ ( B i^i C ) ) = ( ( A \ B ) u. ( A \ C ) ) $=
    ( cin cdif cvv cun dfin3 difeq2i indi dfin2 invdif uneq12i 3eqtr3 eqtr ) AB
    CDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASDAQDZARDZGUAUDAQRJASKUEUBUFUCABLACLM
    NO $.
    $( [18-Sep-04] $) $( [17-Aug-04] $)

  $( Distributive law for class difference. $)
  difindir $p |- ( ( A i^i B ) \ C ) = ( ( A \ C ) i^i ( B \ C ) ) $=
    ( cin cvv cdif inindir invdif ineq12i 3eqtr3 ) ABDZECFZDALDZBLDZDKCFACFZBCF
    ZDABLGKCHMONPACHBCHIJ $.
    $( [19-Sep-04] $) $( [17-Aug-04] $)

  $( DeMorgan's law for union.  Theorem 5.2(13) of [Stoll] p. 19. $)
  undm $p |- ( V \ ( A u. B ) ) = ( ( V \ A ) i^i ( V \ B ) ) $=
    ( cvv difundi ) CABD $.
    $( [31-Aug-04] $) $( [18-Aug-04] $)

  $( DeMorgan's law for intersection.  Theorem 5.2(13') of [Stoll] p. 19. $)
  indm $p |- ( V \ ( A i^i B ) ) = ( ( V \ A ) u. ( V \ B ) ) $=
    ( cvv difindi ) CABD $.
    $( [20-Sep-04] $) $( [18-Aug-04] $)

  $( A relationship involving double difference and union. $)
  difun1 $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) \ C ) $=
    ( cvv cdif cin cun inass invdif undm ineq2i eqtr3 3eqtr3 difeq1i ) ADBEZFZC
    EZABCGZEZABEZCEPDCEZFAOUAFZFZQSAOUAHPCIADREZFUCSUDUBABCJKARILMPTCABINL $.
    $( [4-Sep-04] $) $( [29-Aug-04] $)

  $( Swap second and third argument of double difference. $)
  dif23 $p |- ( ( A \ B ) \ C ) = ( ( A \ C ) \ B ) $=
    ( cun cdif uncom difeq2i difun1 3eqtr3 ) ABCDZEACBDZEABECEACEBEJKABCFGABCHA
    CBHI $.
    $( [27-Aug-04] $) $( [18-Aug-04] $)

  $( Two ways of expressing symmetric difference.   This theorem shows the
     equivalence of the definition of symmetric difference in [Stoll] p. 13
     and the restated definition in Example 4.1 of [Stoll] p. 262. $)
  symdif1 $p |- ( ( A \ B ) u. ( B \ A ) ) = ( ( A u. B ) \ ( A i^i B ) ) $=
    ( cun cin cdif difundir difin incom difeq2i eqtr uneq12i eqtr2 ) ABCABDZEAM
    EZBMEZCABEZBAEZCABMFNPOQABGOBBADZEQMRBABHIBAGJKL $.
    $( [2-Sep-04] $) $( [17-Aug-04] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways of expressing symmetric difference. $)
    symdif2 $p |- ( ( A \ B ) u. ( B \ A ) ) =
                  { x | -. ( x e. A <-> x e. B ) } $=
      ( cv wcel wb wn cdif cun wo wa elun eldif pm4.13 biran bitr ancom bi2or
      orcom dfbi bitr4 nbbn 3bitr biabri ) ADZBEZUECEZFGZABCHZCBHZIZUEUKEUEUIEZ
      UEUJEZJUFGZGZUGGZKZUNUGKZJZUHUEUIUJLULUQUMURULUFUPKUQUEBCMUFUOUPUFNOPUMUG
      UNKURUECBMUGUNQPRUSUNUGFZUHUSURUQJUTUQURSUNUGTUAUFUGUBPUCUD $.
      $( [21-Sep-04] $) $( [17-Aug-04] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    inex1.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation.  Compare
       Exercise 4 of [TakeutiZaring] p. 22. $)
    inex1 $p |- ( A i^i B ) e. V $=
      ( vx vy cin cv wceq wex wel wcel wa wb wal zfaus dfcleq elin bilbi bial
      bitr biex mpbir issetri ) DABFZDGZUDHZDIEDJZEGZAKUHBKZLZMZENZDIUIEDACOUFU
      LDUFUGUHUDKZMZENULEUEUDPUNUKEUMUJUGUHABQRSTUAUBUC $.
      $( [5-Aug-93] $)
  $}

  ${
    inex2.1 $e |- A e. V $.
    $( Separation Scheme (Aussonderung) using class notation. $)
    inex2 $p |- ( B i^i A ) e. V $=
      ( cin cvv incom inex1 eqeltr ) BADABDEBAFABCGH $.
      $( [27-Apr-94] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Closed-form, generalized Separation Scheme. $)
    inex1g $p |- ( A e. C -> ( A i^i B ) e. V ) $=
      ( vx cv cin cvv wcel wceq ineq1 eleq1d visset inex1 vtoclg ) DEZBFZGHABFZ
      GHDACOAIPQGOABJKOBDLMN $.
      $( [7-Apr-95] $)
  $}

  ${
    ssex.1 $e |- B e. V $.
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring]
       p. 22.  This is one way of expressing the Axiom of Separation ~ zfaus
       (a.k.a. Subset Axiom). $)
    ssex $p |- ( A (_ B -> A e. V ) $=
      ( wss cin wceq cvv wcel df-ss inex2 eleq1 mpbii sylbi ) ABDABEZAFZAGHZABI
      ONGHPBACJNAGKLM $.
      $( [27-Apr-94] $)
  $}

  ${
    ssexi.1 $e |- B e. V $.
    ssexi.2 $e |- A (_ B $.
    $( The subset of a set is also a set. $)
    ssexi $p |- A e. V $=
      ( wss cvv wcel ssex ax-mp ) ABEAFGDABCHI $.
      $( [9-Sep-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring]
       p. 22 (generalized). $)
    ssexg $p |- ( B e. C -> ( A (_ B -> A e. V ) ) $=
      ( vx cv wss cvv wcel wi wceq sseq2 birimd visset ssex vtoclg ) ADEZFZAGHZ
      IABFZRIDBCPBJQSRPBAKLAPDMNO $.
      $( [14-Aug-94] $)
  $}

  $( Existence of a difference. $)
  difexg $p |- ( A e. C -> ( A \ B ) e. V ) $=
    ( wcel cdif wss cvv difss ssexg mpi ) ACDABEZAFKGDABHKACIJ $.
    $( [26-May-98] $)

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    zfausab.1 $e |- A e. V $.
    $( Separation Scheme in terms of an abstraction class. $)
    zfausab $p |- { x | ( x e. A /\ ph ) } e. V $=
      ( cv wcel wa cab ssab ssexi ) BECFAGBHCDABCIJ $.
      $( [8-Jun-94] $)
  $}

  ${
    $d x A $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    rabexg $p |- ( A e. B -> { x e. A | ph } e. V ) $=
      ( wcel crab wss cvv ssrab ssexg mpi ) CDEABCFZCGLHEABCILCDJK $.
      $( [23-Oct-99] $)
  $}

  ${
    $d x A $.
    zfausrab.1 $e |- A e. V $.
    $( Separation Scheme in terms of a restricted abstraction class. $)
    rabex $p |- { x e. A | ph } e. V $=
      ( cvv wcel crab rabexg ax-mp ) CEFABCGEFDABCEHI $.
      $( [19-Jul-96] $)
  $}

