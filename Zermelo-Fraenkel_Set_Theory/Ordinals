$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Ord $. $( Ordinal predicate $)
  $c On $. $( The class of ordinals $)
  $c Lim $. $( Limit ordinal predicate $)
  $c suc $. $( Successor function (read:  'successor of') $)

  $( Extend the definition of a wff to include the ordinal predicate. $)
  word $a wff Ord A $.

  $( Extend the definition of a class to include the class of all ordinals.
     (The 0 in the name prevents creating a file called con.html, which
     causes problems in Windows.) $)
  con0 $a class On $.

  $( Extend the definition of a wff to include the limit ordinal predicate. $)
  wlim $a wff Lim A $.

  $( Extend class notation to include the successor function. $)
  csuc $a class suc A $.

  $( Define the ordinal predicate, which is true for a class that is transitive
     and is well-ordered by the epsilon relation.  Variant of definition of
     [BellMachover] p. 468. $)
  df-ord $a |- ( Ord A <-> ( Tr A /\ E We A ) ) $.

  $( Define the class of all ordinals.  Definition 7.11 of [TakeutiZaring]
     p. 38. $)
  df-on $a |- On = { x | Ord x } $.

  $( Define the limit ordinal predicate, which is true for a non-empty ordinal
     that is not a successor (i.e. that is the union of itself).  Our
     definition combines the definition of Lim of [BellMachover] p. 471 and
     Exercise (1) of [TakeutiZaring] p. 42. $)
  df-lim $a |- ( Lim A <-> ( Ord A /\ -. A = (/) /\ A = U. A ) ) $.

  $( Define the successor of a class.  When applied to an ordinal number, the
     successor means the same thing as "plus 1" (see ~ oa1suc ). Definition
     7.22 of [TakeutiZaring] p. 41, who use "+ 1" to denote the operation.
     Our definition is a generalization to classes.  Although it is not
     conventional to use it with proper classes, it has no affect on a proper
     class ( ~ sucprc ), so that the successor of any ordinal class is still
     an ordinal class ( ~ ordsuc ), simplifying certain proofs.  Some authors
     denote the successor operation with a prime (apostrophe-like) symbol,
     such as Definition 6 of [Suppes] p. 134 and the definition of successor
     of [Mendelson] p. 246 (who uses the symbol "Suc" as a predicate to mean
     "is a successor ordinal").  The definition of sucessor of [Enderton]
     p. 68 denotes the operation with a plus-sign superscript.  $)
  df-suc $a |- suc A = ( A u. { A } ) $.

  $( Equality theorem for the ordinal predicate. $)
  ordeq $p |- ( A = B -> ( Ord A <-> Ord B ) ) $=
    ( wceq wtr cep wwe wa word treq weeq2 bi2and df-ord 3bitr4g ) ABCZADZAEFZGB
    DZBEFZGAHBHNOQPRABIABEJKALBLM $.
    $( [17-Sep-93] $)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( An ordinal number is an ordinal set. $)
    elong $p |- ( A e. B -> ( A e. On <-> Ord A ) ) $=
      ( vx cv word con0 ordeq df-on elab2g ) CDZEAECAFBJAGCHI $.
      $( [5-Jun-94] $)
  $}

  ${
    elon.1 $e |- A e. V $.
    $( An ordinal number is an ordinal set. $)
    elon $p |- ( A e. On <-> Ord A ) $=
      ( cvv wcel con0 word wb elong ax-mp ) ACDAEDAFGBACHI $.
      $( [5-Jun-94] $)
  $}

  $( An ordinal number has the ordinal property. $)
  eloni $p |- ( A e. On -> Ord A ) $=
    ( con0 wcel word elong ibi ) ABCADABEF $.
    $( [5-Jun-94] $)

  $( An ordinal number is an ordinal set. $)
  elon2 $p |- ( A e. On <-> ( Ord A /\ A e. V ) ) $=
    ( con0 wcel word cvv wa eloni elisset jca elong biimparc impbi ) ABCZADZAEC
    ZFMNOAGABHIOMNAEJKL $.
    $( [8-Feb-04] $) $( [8-Feb-04] $)

  $( Equality theorem for the limit predicate. $)
  limeq $p |- ( A = B -> ( Lim A <-> Lim B ) ) $=
    ( wceq word c0 wn cuni w3a wlim ordeq cleq1 binegd unieq cleq2d bitrd
    bi3and df-lim 3bitr4g ) ABCZADZAECZFZAAGZCZHBDZBECZFZBBGZCZHAIBISTUEUBUGUDU
    IABJSUAUFABEKLSUDAUHCUISUCUHAABMNABUHKOPAQBQR $.
    $( [22-Apr-94] $)

  $( Epsilon well orders every ordinal.  Proposition 7.4 of [TakeutiZaring]
     p. 36. $)
  ordwe $p |- ( Ord A -> E We A ) $=
    ( word wtr cep wwe df-ord pm3.27bd ) ABACADEAFG $.
    $( [3-Apr-94] $)

  $( An ordinal class is transitive. $)
  ordtr $p |- ( Ord A -> Tr A ) $=
    ( word wtr cep wwe df-ord pm3.26bd ) ABACADEAFG $.
    $( [3-Apr-94] $)

  $( Epsilon is well-founded on an ordinal class. $)
  ordfr $p |- ( Ord A -> E Fr A ) $=
    ( word cep wwe wfr ordwe wefr syl ) ABACDACEAFACGH $.
    $( [22-Apr-94] $)

  $( An element of an ordinal class is a subset of it. $)
  ordelss $p |- ( ( Ord A /\ B e. A ) -> B (_ A ) $=
    ( wtr wcel wss word trss imp ordtr sylan ) ACZBADZBAEZAFKLMABGHAIJ $.
    $( [30-May-94] $)

  $( A transitive subclass of an ordinal class is ordinal. $)
  trssord $p |- ( ( Tr A /\ A (_ B /\ Ord B ) -> Ord A ) $=
    ( wtr wss word wa cep wwe w3a wess imp ordwe sylan5 imlan 3anass df-ord
    3imtr4 ) ACZABDZBEZFZFRAGHZFRSTIAEUAUBRSBGHZUBTSUCUBABGJKBLMNRSTOAPQ $.
    $( [29-May-94] $)

  $( Epsilon irreflexivity of ordinals:  no ordinal is a member of itself.
     Theorem 2.2(i) of [BellMachover] p. 469, generalized to classes.  For
     ordinals, we can prove this without invoking the Axiom of Regularity. $)
  ordeirr $p |- ( Ord A -> -. A e. A ) $=
    ( word cep wfr wcel wn ordfr efrirr syl ) ABACDAAEFAGAHI $.
    $( [2-Jan-94] $)

  $( A member of an ordinal class is not equal to it. $)
  nordeq $p |- ( ( Ord A /\ B e. A ) -> -. A = B ) $=
    ( word wcel wceq wn eleq1 binegd ordeirr syl5bi com12 con2d imp ) ACZBADZAB
    EZFNPOPNOFZPAADZFQNPROABAGHAIJKLM $.
    $( [25-May-98] $)

  $( An ordinal class cannot an element of one of its members.  Variant of
     first part of Theorem 2.2(vii) of [BellMachover] p. 469. $)
  ordn2lp $p |- ( Ord A -> -. ( A e. B /\ B e. A ) ) $=
    ( word wcel wa ordeirr wtr wi ordtr trel syl mtod ) ACZABDBADEZAADZAFMAGNOH
    AIAABJKL $.
    $( [3-Apr-94] $)

  ${
    $d x R $.  $d x A $.  $d x B $.
    $( A subclass (possibly proper) of an ordinal class has a minimal element.
       Proposition 7.5 of [TakeutiZaring] p. 36. $)
    tz7.5 $p |- ( ( Ord A /\ ( B (_ A /\ -. B = (/) ) ) ->
               E. x e. B ( B i^i x ) = (/) ) $=
      ( cep wwe wss c0 wceq wn wa cv cin wrex word wefrc ordwe sylan ) BDECBFCG
      HIJCAKLGHACMBNABCOBPQ $.
      $( [18-Feb-04] $) $( [18-Feb-04] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( An element of an ordinal class is ordinal.  Proposition 7.6 of
       [TakeutiZaring] p. 36. $)
    ordelord $p |- ( ( Ord A /\ B e. A ) -> Ord B ) $=
      ( vx vz vy word wcel cv wa wi wceq eleq1 biland ordeq bi2imd wtr cep wwe
      wel wal w3a wetrep ordwe sylan pm3.26 adantr ordtr trel3 syl 3anrot
      3anass bitr3 syl5ibr exp3a imp31 trel com23 adantrl pm3.27 3jca sylan13
      exp pm2.43d 19.21aivv dftr2 sylibr wss trss wess com12 syld imp jca
      df-ord vtoclg anabsi7 ) AFZBAGZBFZVQCHZAGZIZVTFZJVQVRIZVSJCBAVTBKZWBWDWCV
      SWEWAVRVQVTBALMVTBNOWBVTPZVTQRZIWCWBWFWGWBDESZECSZIZDCSZJZETDTWFWBWLDEWBW
      JWKWBWJWLVQDHZAGZEHZAGZWAUAZWLWBWJIZAQRZWQWLVQDECAUBAUCZUDWBVQWJVQWAUEUFW
      RWNWPWAVQWAWJWNVQWAWJWNVQWHWIWAUAZWNWAWJIZVQAPZXAWNJAUGZAWMWOVTUHUIXAWAWH
      WIUAXBWAWHWIUJWAWHWIUKULUMUNUOWBWIWPWHVQWAWIWPVQWIWAWPVQWIWAWPVQXCWIWAIWP
      JXDAWOVTUPUIUNUQUOURWBWAWJVQWAUSUFUTVAVBVCVDDEVTVEVFVQWAWGVQWAVTAVGZWGVQX
      CWAXEJXDAVTVHUIVQWSXEWGJWTXEWSWGVTAQVIVJUIVKVLVMVTVNVFVOVP $.
      $( [23-Apr-94] $)
  $}

  $( An element of an ordinal class is an ordinal number. $)
  ordelon $p |- ( ( Ord A /\ B e. A ) -> B e. On ) $=
    ( word wcel wa con0 ordelord wb elong adantl mpbird ) ACZBADZEBFDZBCZABGMNO
    HLBAIJK $.
    $( [30-Oct-03] $) $( [26-Oct-03] $)

  $( An element of an ordinal number is an ordinal number.  Theorem 2.2(iii) of
     [BellMachover] p. 469. $)
  onelon $p |- ( ( A e. On /\ B e. A ) -> B e. On ) $=
    ( word wcel con0 ordelon eloni sylan ) ACBADBEDAEDABFAGH $.
    $( [4-Nov-03] $) $( [26-Oct-03] $)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Proposition 7.7 of [TakeutiZaring] p. 37. $)
    tz7.7 $p |- ( ( Ord A /\ Tr B ) ->
                ( B e. A <-> ( B (_ A /\ -. B = A ) ) ) $=
      ( vx vy word wtr wa wcel wss wceq wn wi cep wfr tz7.2 ordtr ordfr jca
      sylan exp adantr cdif c0 cv cin wrex eleq1 biimpd trss eldifi syl5
      difin0ss com12 syl6 syl ad2antll imp32 wel weq w3o wwe wecmpep ordwe
      ssel2 im2an syl2an adantlr biimpcd eldifn nsyli imp adantll adantl trel
      exp3a com23 adantld ecased exp44 com34 imp31 ssrdv adantrr eqssd
      ad2antrl syl13 exp32 r19.23adv difss tz7.5 mpan21 exp4b adantrd pm2.43i
      pssdifn0 syl7 exp4a pm2.43d imp3a impbid ) AEZBFZGZBAHZBAIZBAJKZGZXAXDXGL
      XBXAXDXGAFZAMNZGXDXGXAABOXAXHXIAPZAQRSTUAXCXEXFXDXCXEXFXDLXCXEXEXFXDXCXEA
      BUBZUCJKZXDXGXCXEXLXDLZLZXCXAXNXBXCXEXAXMXCXEXAXLXDXCXEGZXKCUDZUEUCJZCXKU
      FZXDXAXLGXOXQXDCXKXOXPXKHZXQXDXPBJZXPAHZXDXOXSXQGGZXTYAXDXPBAUGUHYBXPBXOX
      SXQXPBIZXAXSXQYCLZLZXBXEXAXHYEXJXHXSXPAIZYDXHYAYFXSAXPUIXPABUJZUKXQYFYCAB
      XPULUMUNUOUPUQXOXSBXPIXQXOXSGDBXPXCXEXSDUDZBHZDCURZLXCXEYIXSYJXCXEYIXSYJX
      CXEYIGZXSGZGYJDCUSZCDURZXAYLYJYMYNUTZXBAMVAYHAHZYAGYOXAYLDCAVBAVCYKYPXSYA
      BAYHVDYGVEVFVGYLYMKZXCYIXSYQXEYIXSYQYIYMXPBHZXSYMYIYRYHXPBUGVHXPABVIZVJVK
      VLVMXBYLYNKZXAXBYKXSYTXBYIXSYTLZXEXBYIUVAXBYIGYNYRXSXBYIYNYRLXBYNYIYRXBYN
      YIYRBXPYHVNVOVPVKYSVJTVQUQVLVRVSVTWAWBWCWDXSYAXOXQYGWEWFWGWHXAXKAIXLXRABW
      ICAXKWJWKUKWLVPWMWNBAWOWPWQWRWSWT $.
      $( [5-May-94] $)
  $}

  $( Corollary 7.8 of [TakeutiZaring] p. 37. $)
  ordelssne $p |- ( ( Ord A /\ Ord B ) ->
              ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
    ( word wcel wss wceq wn wa wb wtr tz7.7 ordtr sylan5 ancoms ) BCZACZABDABEA
    BFGHIZOAJQPBAKALMN $.
    $( [25-Nov-95] $)

  $( Corollary 7.8 of [TakeutiZaring] p. 37. $)
  ordelpss $p |- ( ( Ord A /\ Ord B ) -> ( A e. B <-> A (. B ) ) $=
    ( word wa wcel wss wceq wn wpss ordelssne dfpss2 syl6bbr ) ACBCDABEABFABGHD
    ABIABJABKL $.
    $( [17-Jun-98] $)

  $( For ordinals, subclass is equivalent to membership or equality. $)
  ordsseleq $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wa wss wcel wceq wo wn wi ordelssne biimprd exp3a orcom df-or bitr
    syl6ibr pm3.26 syl6bi eqimss jctir jaob sylibr impbid ) ACBCDZABEZABFZABGZH
    ZUEUFUHIZUGJZUIUEUFUJUGUEUGUFUJDZABKZLMUIUHUGHUKUGUHNUHUGOPQUEUGUFJZUHUFJZD
    UIUFJUEUNUOUEUGULUFUMUFUJRSABTUAUGUFUHUBUCUD $.
    $( [25-Nov-95] $)

  $( Proposition 7.9 of [TakeutiZaring] p. 37. $)
  ordin $p |- ( ( Ord A /\ Ord B ) -> Ord ( A i^i B ) ) $=
    ( word cin wtr wa wss inss2 trssord mp3an2 trin ordtr syl2an sylan anabss3
    ) ACZBCZABDZCZREZQSPQFTRBGQSABHRBIJAEBETPQABKALBLMNO $.
    $( [9-May-94] $)

  ${
    $d x y A $.  $d x y B $.
    $( The intersection of two ordinal numbers is an ordinal number. $)
    onin $p |- ( ( A e. On /\ B e. On ) -> ( A i^i B ) e. On ) $=
      ( con0 wcel wa cin word ordin eloni syl2an cvv wb pm3.26 inex1g elong
      3syl mpbird ) ACDZBCDZEZABFZCDZUAGZAGBGUCRSABHAIBIJTRUAKDUBUCLRSMABCNUAKO
      PQ $.
      $( [7-Apr-95] $)
  $}

  $( A trichotomy law for ordinals. Proposition 7.10 of [TakeutiZaring]
     p. 38. $)
  ordtri3or $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ A = B \/ B e. A ) ) $=
    ( word wa wcel wceq wo w3o wss cin wn ordin ordeirr syl elin incom eleq1i
    bilan bitr bineg ianor sylib inss1 ordsseleq mpbii sylan anabss1 ord df-ss
    syl6ibr anabss4 im2ord mpd wb ancoms bi2ord mpbid df-3or oridm cleqcom
    bilor bitr3 or23 or4 3bitr4 sylibr ) ACZBCZDZABEZABFZGZBAEZBAFZGZGZVJVKVMHZ
    VIABIZBAIZGZVPVIABJZAEZKZBAJZBEZKZGZVTVIWAWAEZKZWGVIWACZWIABLZWAMNWIWBWEDZK
    WGWHWLWHWBWABEZDWLWAABOWMWEWBWAWDBABPQRSTWBWEUASUBVIWCVRWFVSVIWCWAAFZVRVIWB
    WNVGVHWBWNGZWJVGWOVIWJVGDWAAIWOABUCWAAUDUEWKUFUGUHABUIUJVIWFWDBFZVSVIWEWPVG
    VHWEWPGZWDCZVHWQVHVGDWRVHDWDBIWQBAUCWDBUDUEBALUFUKUHBAUIUJULUMVIVRVLVSVOABU
    DVHVGVSVOUNBAUDUOUPUQVQVLVMGZVPVJVKVMURVJVMGZVKGWTVKVNGZGWSVPVKXAWTVKVKVKGX
    AVKUSVKVNVKABUTVAVBVAVJVKVMVCVJVKVMVNVDVESVF $.
    $( [10-May-94] $)

  $( A trichotomy law for ordinals. $)
  ordtri1 $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> -. B e. A ) ) $=
    ( word wa wss wcel wceq wo wn ordsseleq wi ordn2lp imnan sylibr eleq2
    binegd ordeirr syl5bir com12 jaao w3o ordtri3or df-3or orcom df-or 3bitr
    sylib impbid bitrd ) ACZBCZDZABEABFZABGZHZBAFZIZABJULUOUQUJUMUQUKUNUJUMUPDI
    UMUQKABLUMUPMNUNUKUQUNUQBBFZIUKUNUPURABBOPBQRSTULUMUNUPUAZUQUOKZABUBUSUOUPH
    UPUOHUTUMUNUPUCUOUPUDUPUOUEUFUGUHUI $.
    $( [25-Mar-95] $)

  $( A trichotomy law for ordinal numbers. $)
  ontri1 $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B <-> -. B e. A ) ) $=
    ( word wss wcel wn wb con0 ordtri1 eloni syl2an ) ACBCABDBAEFGAHEBHEABIAJBJ
    K $.
    $( [14-Nov-03] $) $( [6-Nov-03] $)

  $( A trichotomy law for ordinals. $)
  ordtri2 $p |- ( ( Ord A /\ Ord B ) ->
               ( A e. B <-> -. ( A = B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn wb wss ordsseleq cleqcom bilor orcom bitr syl6bb
    ordtri1 bitr3d ancoms bicon2d ) ACZBCZDABEZBAFZGZABFZUBUAUEUFHZIUBUADZBAJZU
    EUGUHUIUDBAEZGZUEBAKUKUDUCGUEUJUCUDBALMUDUCNOPBAQRST $.
    $( [25-Nov-95] $)

  $( A trichotomy law for ordinals. $)
  ordtri3 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> -. ( A e. B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn eleq2 binegd ordeirr syl5bi syl5bir im2and ioran
    syl6ibr com12 w3o wi ordtri3or df-3or or23 df-or 3bitr sylib impbid ) ACZBC
    ZDZABEZABFZBAFZGZHZUJUIUNUJUIUKHZULHZDUNUJUGUOUHUPUJAAFZHUOUGUJUQUKABAIJAKL
    UJUPBBFZHUHUJULURABBIJBKMNUKULOPQUIUKUJULRZUNUJSZABTUSUKUJGULGUMUJGUTUKUJUL
    UAUKUJULUBUMUJUCUDUEUF $.
    $( [18-Oct-95] $)

  $( A trichotomy law for ordinals. $)
  ordtri4 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> ( A (_ B /\ -. A e. B ) ) ) $=
    ( word wa wceq wcel wo wn wss ordtri3 ordtri1 birand ancom ioran bitr4
    syl6bb bitr4d ) ACBCDZABEABFZBAFZGHZABIZSHZDZABJRUDTHZUCDZUARUBUEUCABKLUFUC
    UEDUAUEUCMSTNOPQ $.
    $( [5-Nov-03] $) $( [1-Nov-03] $)

  $( An ordinal class and its singleton are disjoint. $)
  orddisj $p |- ( Ord A -> ( A i^i { A } ) = (/) ) $=
    ( word wcel wn csn cin c0 wceq ordeirr disjsn sylibr ) ABAACDAAEFGHAIAAJK
    $.
    $( [19-May-98] $)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( The ordinal class is founded.  This lemma is needed for
       ~ ordon in order to eliminate the need for the Axiom of Regularity. $)
    onfr $p |- E Fr On $=
      ( vx vz vy con0 cep wfr cv wss c0 wceq wn wa cin wrex wi dfepfr wel wex
      weq ineq2 cleq1d rcla4ev exp com12 a1d word wcel ssel visset elon syl6ib
      wtr inss2 inex1 epfrc mpani ax-17 hbre1 inss1 sseli trss syl5 sseqin2
      inass syl6eqr sylbi biimprcd sylan9 imp im2an exp32 pm2.43b com23 ra4e
      syl8 r19.23ad ordfr ordtr sylan13 syl6 com3r pm2.61i 19.23adv n0 syl5ib
      mpgbir ) DEFAGZDHZWGIJKZLWGBGZMZIJZBWGNZOAABDPWHWIWMWHCAQZCRWMWIWHWNWMCWG
      CGZMZIJZWHWNWMOZOWQWRWHWNWQWMWNWQWMWLWQBWOWGBCSWKWPIWJWOWGTUAUBUCUDUEWHWN
      WQKZWMWHWNWOUFZWSWMOZWHWNWODUGWTWGDWOUHWOCUIUJUKWOEFZWOULZXAWTXBWSWPWJMZI
      JZBWPNZXCWMXBWPWOHZWSXFWGWOUMZXBXGWSLXFBWOWPWGWOAUIUNUOUCUPXCXEWMBWPXCBUQ
      WLBWGURXCWJWPUGZXEBAQZWLLZWMXCXEXIXKXCXEXIXKOXCXELZXIXKXIXLXIXKXIXJXLXILW
      LWPWGWJWGWOUSUTXLXIWLXCXIWJWOHZXEWLXCBCQXMXIWOWJVAWPWOWJXHUTVBXMWLXEXMWKX
      DIXMWJWOWJMZJZWKXDJWJWOVCXOWKWGXNMXDWJXNWGTWGWOWJVDVEVFUAVGVHVIVJVKVLUCVM
      WLBWGVNVOVPVHWOVQWOVRVSVTWAWBWCCWGWDWEVIWF $.
      $( [17-May-94] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The class of all ordinal numbers is ordinal.  Proposition 7.12 of
       [TakeutiZaring] p. 38, but without using the Axiom of Regularity. $)
    ordon $p |- Ord On $=
      ( vx vy con0 word wtr cep wwe wa cv wss dftr3 wcel wel ordelord visset
      elon sylanb exp syl6ibr ssrdv mprgbir wfr wbr weq w3o wral onfr
      ordtri3or epel pm4.2 bi3or sylibr eloni syl2an rgen2 pm3.2i dfwe2 mpbir
      df-ord ) CDCEZCFGZHUTVAUTAIZCJACACKVBCLZBVBCVCBAMZBIZDZVECLZVCVDVFVBDZVDV
      FVCVBVENVBAOPQRVEBOPSTUAVACFUBZVBVEFUCZABUDZVEVBFUCZUEZBCUFACUFZHVIVNUGVM
      ABCVHVFVMVCVGVHVFHABMZVKVDUEVMVBVEUHVJVOVKVKVLVDABUIVKUJBAUIUKULVBUMVEUMU
      NUOUPABCFUQURUPCUSUR $.
      $( [17-May-94] $)
  $}

  $( The epsilon relation well-orders the class of ordinal numbers.
     Proposition 4.8(g) of [Mendelson] p. 244. $)
  epweon $p |- E We On $=
    ( con0 word cep wwe ordon ordwe ax-mp ) ABACDEAFG $.
    $( [1-Nov-03] $) $( [1-Nov-03] $)

  $( ` x ` is dummy. $)
  $( No set contains all ordinal numbers.  Proposition 7.13 of
     [TakeutiZaring] p. 38, but without using the Axiom of Regularity.  This
     is also known as the Burali-Forti paradox (remark of [Enderton] p. 194).
     In 1897, Cesare Burali-Forti noticed that since the "set" of all
     ordinals is ordinal ( ~ ordon ), it must be both an element of the
     set of all ordinals yet greater than every such element.  ZF set theory
     resolves this paradox by not allowing the class of all ordinal numbers to
     be a set (so instead it is a proper class).  Here we prove the denial of
     its existence. $)
  onprc $p |- -. On e. V $=
    ( con0 cvv wcel word wn ordon ordeirr ax-mp elong mpbiri mto ) ABCZAACZADZM
    EFAGHLMNFABIJK $.
    $( [18-May-94] $)

  $( A way to express the ordinal property of a class in terms of the class
     of ordinal numbers.  Corollary 7.14 of [TakeutiZaring] p. 38 and its
     converse. $)
  ordeleqon $p |- ( Ord A <-> ( A e. On \/ A = On ) ) $=
    ( word con0 wcel wceq wo cvv onprc elisset mto w3o ordon ordtri3or mpan2
    df-3or sylib ord mt3i eloni ordeq mpbiri jaoi impbi ) ABZACDZACEZFZUDUGCADZ
    UHCGDHCAIJUDUGUHUDUEUFUHKZUGUHFUDCBZUILACMNUEUFUHOPQRUEUDUFASUFUDUJLACTUAUB
    UC $.
    $( [1-Jun-03] $)

  $( Any ordinal class is a subclass of the class of ordinal numbers.
     Corollary 7.15 of [TakeutiZaring] p. 38. $)
  ordsson $p |- ( Ord A -> A (_ On ) $=
    ( word con0 wcel wss wceq wn wa wb ordon ordelssne mpan2 pm3.26 syl6bi wo
    ordeleqon biimp ord eqimss syl6 pm2.61d ) ABZACDZACEZUBUCUDACFZGZHZUDUBCBUC
    UGIJACKLUDUFMNUBUCGUEUDUBUCUEUBUCUEOAPQRACSTUA $.
    $( [18-May-94] $)

  $( An ordinal number is a subset of the class of ordinal numbers. $)
  onsst $p |- ( A e. On -> A (_ On ) $=
    ( con0 wcel word wss eloni ordsson syl ) ABCADABEAFAGH $.
    $( [5-Jun-94] $)

  ${
    $d x A $.
    $( The Principle of Transfinite Induction.  Theorem 7.17 of [TakeutiZaring]
       p. 39.  This principle states that if ` A ` is a class of ordinals with
       the property that every ordinal number that is a subset of ` A ` also
       belongs to ` A ` , then every ordinal is in ` A ` . $)
    tfi $p |- ( ( A (_ On /\ A. x e. On ( x (_ A -> x e. A ) ) -> A = On ) $=
      ( con0 wss cv wcel wi wral wa wceq cdif cin c0 wrex wn wal eldifn adantl
      onsst difin0ss com12 syl syl4d a2i eldifi syl5 imp mtod exp 19.20i
      df-ral 3imtr4 ralnex sylib ssdif0 bineg difss word ordon tz7.5 mpan
      sylbi nsyl2 imlan eqss sylibr ) BCDZAEZBDZVHBFZGZACHZIVGCBDZIBCJVLVMVGVLC
      BKZVHLMJZAVNNZVMVLVOOZAVNHZVPOVHCFZVKGZAPVHVNFZVQGZAPVLVRVTWBAVTWAVQVTWAI
      VOVJWAVJOVTVHCBQRVTWAVOVJGZVTVSWCWAVSVKWCVSVOVIVJVSVHCDZVOVIGVHSVOWDVICBV
      HTUAUBUCUDVHCBUEUFUGUHUIUJVKACUKVQAVNUKULVOAVNUMUNVMOVNMJZOZVPVMWECBUOUPV
      NCDZWFVPCBUQCURWGWFIVPUSACVNUTVAVAVBVCVDBCVEVF $.
      $( [29-Feb-04] $) $( [18-Feb-04] $)
  $}

  ${
    $d y ph z $.  $d x y z $.  $( ` z ` is dummy. $)
    tfis.1 $e |- ( x e. On -> ( A. y e. x [ y / x ] ph -> ph ) ) $.
    $( Transfinite Induction Schema.  If all ordinal numbers less than a
       given number ` x ` have a property (induction hypothesis), then all
       ordinal numbers have the property (conclusion).  Exercise 25 of
       [Enderton] p. 200. $)
    tfis $p |- ( x e. On -> ph ) $=
      ( vz cv con0 wcel crab wss wi wral wceq ssrab wsb wa ax-17 wel hbs1
      hbral hbim weq eleq1 raleq sbequ12 bi2imd chv2 dfss3 elrabsf pm3.27bd
      r19.20si sylbi syl5 anc2li syl6ibr rgen tfi mp2an cleqcomi cleqrabi ) BFZ
      GHZVBAABGGABGIZGVCGJEFZVCJZVDVCHZKZEGLVCGMABGNVGEGVDGHZVEVHABEOZPVFVHVEVI
      VHABCOZCVDLZVIVEVBVJCVALZAKZKVHVKVIKZKBEVHVNBVHBQZVKVIBVJBCVDCERBQABCSTAB
      ESUAUABEUBZVBVHVMVNVAVDGUCVPVLVKAVIVJCVAVDUDABEUEUFUFDUGVECFZVCHZCVDLVKCV
      DVCUHVRVJCVDVRVQGHVJABEVQGVOUIUJUKULUMUNABEVDGVOUIUOUPEVCUQURUSUTUJ $.
      $( [1-Mar-04] $) $( [1-Aug-94] $)
  $}

  ${
    $d y ph $.  $d x y $.
    tfis2f.1 $e |- ( ps -> A. x ps ) $.
    tfis2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2f.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2f $p |- ( x e. On -> ph ) $=
      ( cv con0 wcel wral wsb sbie biral syl5ib tfis ) ACDCHZIJBDQKAACDLZDQKGRB
      DQABCDEFMNOP $.
      $( [1-Mar-04] $) $( [18-Aug-94] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x y $.
    tfis2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2.2 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2 $p |- ( x e. On -> ph ) $=
      ( ax-17 tfis2f ) ABCDBCGEFH $.
      $( [1-Mar-04] $) $( [18-Aug-94] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x ch $.  $d x A $.  $d x y $.
    tfis3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis3.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    tfis3.3 $e |- ( x e. On -> ( A. y e. x ps -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis3 $p |- ( A e. On -> ch ) $=
      ( con0 tfis2 vtoclga ) ACDFJHABDEGIKL $.
      $( [6-Nov-03] $) $( [4-Nov-03] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The union of a class of ordinal numbers is ordinal.  Proposition 7.19 of
       [TakeutiZaring] p. 40. $)
    ssorduni $p |- ( A (_ On -> Ord U. A ) $=
      ( vx vy cuni wtr con0 wss word ordon trssord 3exp mpii cv wral wel wrex
      wcel wa wi ssel eloni ordtr trss 3syl syl6 anc2r syl ssuni syl8
      r19.23adv eluni2 syl5ib r19.21aiv dftr3 sylibr ordelord exp visset elon
      syl6ibr ssrdv syl13 ) ADZEZVCFGZVCHZAFGZVDVEFHZVFIVDVEVHVFVCFJKLVGBMZVCGZ
      BVCNVDVGVJBVCVGBCOZCAPZVJVIVCQZVGVKVJCAVGCMZAQZVKVIVNGZVORZVJVGVOVKVPSZSV
      OVKVQSSVGVOVNFQZVRAFVNTZVSVNHZVNEVRVNUAZVNUBVNVIUCUDUEVOVKVPUFUGVIVNAUHUI
      UJCVIAUKZULUMBVCUNUOVGBVCFVGVLVIFQZVMVGVKWDCAVGVOVSVKWDSZVTVSWAWEWBWAVKVI
      HZWDWAVKWFVNVIUPUQVIBURUSUTUGUEUJWCULVAVB $.
      $( [30-May-94] $)
  $}

  $( The union of a set of ordinal numbers is an ordinal number.  Theorem 9 of
     [Suppes] p. 132. $)
  onunit $p |- ( A e. B -> ( A (_ On -> U. A e. On ) ) $=
    ( wcel cuni con0 word wss cvv wb uniexg elong syl ssorduni syl5bir ) ABCZAD
    ZECZPFZAEGOPHCQRIABJPHKLAMN $.
    $( [3-Nov-03] $) $( [1-Nov-03] $)

  ${
    onuni.1 $e |- A e. V $.
    $( The union of a set of ordinal numbers is an ordinal number. Corollary
       7N(d) of [Enderton] p. 193. $)
    onuni $p |- ( A (_ On -> U. A e. On ) $=
      ( cvv wcel con0 wss cuni wi onunit ax-mp ) ACDAEFAGEDHBACIJ $.
      $( [20-Sep-03] $)
  $}

  $( The union of an ordinal class is ordinal. $)
  uniord $p |- ( Ord A -> Ord U. A ) $=
    ( word con0 wss cuni ordsson ssorduni syl ) ABACDAEBAFAGH $.
    $( [12-Sep-03] $)

  $( Relationship between membership and proper subset of an ordinal number. $)
  onelpsst $p |- ( ( A e. On /\ B e. On ) ->
               ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
    ( word wcel wss wceq wn wa wb con0 ordelssne eloni syl2an ) ACBCABDABEABFGH
    IAJDBJDABKALBLM $.
    $( [15-Sep-95] $)

  $( Relationship between subset and membership of an ordinal number. $)
  onsseleq $p |- ( ( A e. On /\ B e. On ) ->
                 ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wss wcel wceq wo wb con0 ordsseleq eloni syl2an ) ACBCABDABEABFGHAIE
    BIEABJAKBKL $.
    $( [15-Sep-95] $)

  $( An element of an ordinal number is a subset of the number. $)
  onelsst $p |- ( A e. On -> ( B e. A -> B (_ A ) ) $=
    ( con0 wcel word wtr wss wi eloni ordtr trss 3syl ) ACDAEAFBADBAGHAIAJABKL
    $.
    $( [5-Jun-94] $)

  $( Transitive law for ordinal numbers.  Theorem 7M(b) of [Enderton]
     p. 192. $)
  ontr $p |- ( C e. On -> ( ( A e. B /\ B e. C ) -> A e. C ) ) $=
    ( con0 wcel word wtr wa wi eloni ordtr trel 3syl ) CDECFCGABEBCEHACEICJCKCA
    BLM $.
    $( [11-Aug-94] $)

  $( Transitive law for ordinal numbers.  Exercise 3 of [TakeutiZaring]
     p. 40. $)
  ontr2 $p |- ( ( A e. On /\ C e. On ) ->
              ( ( A (_ B /\ B e. C ) -> A e. C ) ) $=
    ( con0 wcel wa wss wi wceq wo onsseleq biimpd ontr exp3a eleq1a com12 a1i
    jaod com23 imp syl9 exp onelon syl5 pm2.43d imp3a ) ADEZCDEZFZABGZBCEZACEZU
    IUKUJULUGUHUKUJULHZHUGUHUKUMUGUHUKFZUMUGBDEZUNUMHZUNUGUOUPUGUOFZUJABEZABIZJ
    ZUNULUQUJUTABKLUHUKUTULHUHUTUKULUHURUKULHZUSUHURUKULABCMNUSVAHUHUKUSULBCAOP
    QRSTUAUBCBUCUDUENTSUF $.
    $( [6-Nov-03] $) $( [6-Nov-03] $)

  ${
    $d x y z A $.
    $( The intersection (infimum) of a non-empty class of ordinal numbers
       belongs to the class.  Compare Exercise 4 of [TakeutiZaring] p. 45. $)
    onint $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. A ) $=
      ( vx vy vz con0 wss c0 wceq wn cint wcel cv cin wrex wa wi wel wral ssel
      ontri1 syl6bir exp sylan9 com4r imp31 r19.20dva disj visset elint2
      3imtr4g imdistani sylan5 exp32 com4l imp32 ssrdv intss1 ad2antrl eqssd
      eleq1d biimpd com34 pm2.43d r19.23adv word ordon tz7.5 mpan syl5 anabsi5
      ) AEFZAGHIZAJZAKZVKABLZMGHZBANZVNVKVLOZVKVPVNBAVKVOAKZVPVNPVKVSVPVSVNVKVS
      VPVSVNPVKVSVPOOZVSVNVTVOVMAVTVOVMVTCVOVMVKVSVPCBQZCLZVMKZPWAVKVSVPWCWAVKV
      SVPWCPZWAVKVOEKZOZWDVKVSOWAWFOZDBQIZDARCDQZDARVPWCWGWHWIDAWAWFDLZAKZWHWIP
      WFWKWHWAWIVKWKWJEKZWEWHWAWIPZPZAEWJSWEWLWNWEWLOWHVOWJFWMVOWJTVOWJWBSUAUBU
      CUDUEUFDAVOUGDWBACUHUIUJVKVSWEAEVOSUKULUMUNUOUPVSVMVOFVKVPVOAUQURUSUTVAUM
      VBVCVDEVEVRVQVFBEAVGVHVIVJ $.
      $( [31-Jan-97] $)
  $}

  $( The intersection of a class of ordinal numbers is zero iff the class
     contains zero. $)
  onint0 $p |- ( A (_ On -> ( |^| A = (/) <-> (/) e. A ) ) $=
    ( con0 wss cint c0 wceq wcel wa wn onint cvv 0ex eleq1 mpbiri intex sylibr
    sylan5 wb adantl mpbid exp wi int0el a1i impbid ) ABCZADZEFZEAGZUFUHUIUFUHH
    UGAGZUIUFAEFIZUJUHAJUHUGKGZUKUHULEKGLUGEKMNAOPQUHUJUIRUFUGEAMSTUAUIUHUBUFAU
    CUDUE $.
    $( [14-May-04] $) $( [24-Apr-04] $)

  ${
    $d x y A $.
    $( A non-empty class of ordinal numbers has a smallest member.  Exercise
       9 of [TakeutiZaring] p. 40. $)
    onssmin $p |- ( ( A (_ On /\ -. A = (/) ) ->
                  E. x e. A A. y e. A x (_ y ) $=
      ( con0 wss c0 wceq wn wa cint wcel cv wral wrex onint intss1 rgen jctir
      sseq1 biraldv rcla4ev syl ) CDECFGHIZCJZCKZUDBLZEZBCMZIALZUFEZBCMZACNUCUE
      UHCOUGBCUFCPQRUKUHAUDCUIUDGUJUGBCUIUDUFSTUAUB $.
      $( [6-Dec-03] $) $( [3-Oct-03] $)
  $}

  ${
    $d x y $.  $d y ph $.
    onminsb.1 $e |- ( ps -> A. x ps ) $.
    onminsb.2 $e |- ( x = |^| { x e. On | ph } -> ( ph <-> ps ) ) $.
    $( If a property is true for some ordinal number, it is true for a minimal
       ordinal number.  This version uses implicit substitution.  Theorem
       Schema 62 of [Suppes] p. 228. $)
    onminsb $p |- ( E. x e. On ph -> ps ) $=
      ( vy con0 wrex crab cint wcel c0 wceq wn rnoel3 wss ssrab onint mpan
      sylbir hbrab1 hbint cv ax-17 elrabf pm3.27bd syl ) ACGHZACGIZJZUIKZBUHUIL
      MNZUKACGOUIGPULUKACGQUIRSTUKUJGKBABCFUJGCFUIACFGUAUBFUCGKCUDDEUEUFUG $.
      $( [3-Oct-03] $) $( [3-Oct-03] $)
  $}

  ${
    $d x y $.
    $( If a property is true for some ordinal number, it is true for a minimal
       ordinal number.  This version uses explicit substitution.  Theorem
       Schema 62 of [Suppes] p. 228. $)
    onminesb $p |- ( E. x e. On ph -> [ |^| { x e. On | ph } / x ] ph ) $=
      ( vy con0 wrex crab cint wcel wsbc c0 wceq wn rnoel3 wss ssrab onint
      mpan sylbir cv ax-17 elrabsf pm3.27bd syl ) ABDEZABDFZGZUEHZABUFIZUDUEJKL
      ZUGABDMUEDNUIUGABDOUEPQRUGUFDHUHABCUFDCSDHBTUAUBUC $.
      $( [29-Sep-03] $)
  $}

  ${
    $d x ps $.  $d x A $.
    onintss.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( If a property is true for an ordinal number, then the minimum ordinal
       number for which it is true is smaller or equal.  Theorem Schema 61 of
       [Suppes] p. 228. $)
    onintss $p |- ( A e. On -> ( ps -> |^| { x e. On | ph } (_ A ) ) $=
      ( con0 wcel crab cint wss wa elrab intss1 sylbir exp ) DFGZBACFHZIDJZPBKD
      QGRABCDFELDQMNO $.
      $( [3-Oct-03] $) $( [3-Oct-03] $)
  $}

  $( The intersection of a non-empty collection of ordinals is an ordinal
     number.  Compare Exercise 6 of [TakeutiZaring] p. 44. $)
  oninton $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. On ) $=
    ( con0 wss c0 wceq wn cint wcel onint exp ssel syld imp ) ABCZADEFZAGZBHZNO
    PAHZQNORAIJABPKLM $.
    $( [29-Jan-97] $)

  $( The intersection of a non-empty abstraction class of ordinals exists
     iff it is an ordinal number. $)
  onintrab $p |- ( |^| { x e. On | ph } e. V <-> |^| { x e. On | ph } e. On ) $=
    ( con0 crab cint cvv wcel c0 wceq wn intex wss ssrab oninton mpan sylbir
    elisset impbi ) ABCDZEZFGZTCGZUASHIJZUBSKSCLUCUBABCMSNOPTCQR $.
    $( [6-Nov-03] $) $( [6-Nov-03] $)

  $( An existence condition equivalent to an intersection's being an ordinal
     number. $)
  onintrab2 $p |- ( E. x e. On ph <-> |^| { x e. On | ph } e. On ) $=
    ( con0 wrex crab cint cvv wcel intexrab onintrab bitr ) ABCDABCEFZGHLCHABCI
    ABJK $.
    $( [6-Nov-03] $) $( [6-Nov-03] $)

  $( No member of a set of ordinal numbers belongs to its minimum. $)
  onnmin $p |- ( ( A (_ On /\ B e. A ) -> -. B e. |^| A ) $=
    ( con0 wss wcel wa cint wn intss1 adantl wb ontri1 c0 wceq oninton noel2
    sylan5 ssel2 sylan13 mpbid ) ACDZBAEZFZAGZBDZBUDEHZUBUEUABAIJUDCEZBCEUEUFKU
    CUDBLUAAMNHUGUBAOABPQACBRST $.
    $( [2-Feb-97] $)

  ${
    $d x A $.  $d x ps $.
    onnminsb.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An ordinal number smaller than the minimum of a set of ordinal numbers
       does not have the property determining that set.  ` ps ` is the wff
       resulting from the substitution of ` A ` for ` x ` in wff ` ph ` . $)
    onnminsb $p |- ( A e. On -> ( A e. |^| { x e. On | ph } -> -. ps ) ) $=
      ( con0 wcel crab cint wn wa elrab wss ssrab onnmin mpan sylbir exp con2d
      ) DFGZBDACFHZIGZTBUBJZTBKDUAGZUCABCDFELUAFMUDUCACFNUADOPQRS $.
      $( [9-Nov-03] $) $( [9-Nov-03] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( A way to show that an ordinal number equals the minimum of a collection
       of ordinal numbers: it must be in the collection, and it must not be
       larger than any member of the collection. $)
    oneqmini $p |- ( B (_ On -> ( ( A e. B /\ A. x e. A -. x e. B )
                    -> A = |^| B ) ) $=
      ( con0 wss wcel cv wn wral wa cint wceq wi wb ssel im2and ontri1 syl6
      exp3a imp pm5.74d bi2.03 syl6bb biraldv2 ssint syl5bb biimprd exp imp3a
      intss1 a1i adantrd jcad eqss syl6ibr ) CDEZBCFZAGZCFZHZABIZJZBCKZEZVCBEZJ
      BVCLUPVBVDVEUPUQVAVDUPUQVAVDMUPUQJZVDVAVFBUREZACIVAVDVFVGUTACBVFUSVGMUSUR
      BFZHZMVHUTMVFUSVGVIUPUQUSVGVINZMUPUQUSVJUPUQUSJBDFZURDFZJVJUPUQVKUSVLCDBO
      CDUROPBURQRSTUAUSVHUBUCUDABCUEUFUGUHUIUPUQVEVAUQVEMUPBCUJUKULUMBVCUNUO $.
      $( [14-Nov-03] $) $( [14-Nov-03] $)

    $( A way to show that an ordinal number equals the minimum of a non-empty
       collection of ordinal numbers: it must be in the collection, and it must
       not be larger than any member of the collection. $)
    oneqmin $p |- ( ( B (_ On /\ -. B = (/) ) -> ( A = |^| B <->
                  ( A e. B /\ A. x e. A -. x e. B ) ) ) $=
      ( con0 wss c0 wceq wn wa cint wcel cv wral eleq1 onint syl5bir com12 wi
      eleq2 biimpd onnmin exp con2d syl9r r19.21adv adantr jcad oneqmini
      impbid ) CDEZCFGHZIZBCJZGZBCKZALZCKZHZABMZIZULUNUOUSUNULUOUNUOUMCKULBUMCN
      COPQUJUNUSRUKUJUNURABUNUPBKZUPUMKZUJURUNVAVBBUMUPSTUJUQVBUJUQVBHCUPUAUBUC
      UDUEUFUGUJUTUNRUKABCUHUFUI $.
      $( [14-Nov-03] $) $( [14-Nov-03] $)
  $}

  ${
    $d x y A $.
    bm2.5ii.1 $e |- A e. V $.
    $( Problem 2.5(ii) of [BellMachover] p. 471. $)
    bm2.5ii $p |- ( A (_ On -> U. A = |^| { x e. On | A. y e. A y (_ x } ) $=
      ( con0 wss cuni wcel cv wral crab cint wceq onuni intmin wb unissb a1i
      birabi inteqi syl6eq syl ) CEFCGZEHZUCBIAIZFBCJZAEKZLZMCDNUDUCUCUEFZAEKZL
      UHAUCEOUJUGUIUFAEUIUFPUEEHBCUEQRSTUAUB $.
      $( [20-Sep-03] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    onminex.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( If a wff is true for an ordinal number, there is a smallest ordinal
       number for which it is true. $)
    onminex $p |- ( E. x e. On ph -> E. x e. On ( ph /\ A. y e. x -. ps ) ) $=
      ( cv con0 wcel wa wex wn wral wrex cab c0 wceq wel wi wal cint hbab1
      hbint hbel ax-17 hbim hbal hban eleq1 eleq2 birimd bialdv bi2and cla4egf
      anabsi5 wss ssab onint mpan oninton onelon exp syl visset weq elab
      onnmin sylbir con2d syl6 pm2.43d 19.21aiv sylan13 noel3 abid bicom
      df-ral bi2an anass bitr3 biex 3imtr3 df-rex 3imtr4 ) CFZGHZAIZCJZWEABKZDW
      DLZIZIZCJZACGMWJCGMWFCNZOPKZWDWMHZDCQZWHRZDSZIZCJZWGWLWMTZWMHZDFZXAHZWHRZ
      DSZWTWNXBXFWTWSXBXFICDXAWMCDWMWFCDUAZUBZXBXFCCDDXAWMXHXGUCXECDXDWHCXHWHCU
      DUEUFUGWDXAPZWOXBWRXFWDXAWMUHXIWQXEDXIWPXDWHWDXAXCUIUJUKULUMUNWMGUOZWNXBA
      CGUPZWMUQURWNXEDWNXDWHWNXDXCGHZXEWNXAGHZXDXLRXJWNXMXKWMUSURXMXDXLXAXCUTVA
      VBXLBXDXLBXDKZXLBIZXCWMHZXNWFXOCXCDVCCDVDWEXLABWDXCGUHEULVEXJXPXNXKWMXCVF
      URVGVAVHVIVJVKVLWFCVMWSWKCWSWFWIIWKWFWOWIWRWOWFWFCVNVOWHDWDVPVQWEAWIVRVSV
      TWAACGWBWJCGWBWC $.
      $( [2-Feb-97] $)
  $}

  $( The empty set is an ordinal class. $)
  ord0 $p |- Ord (/) $=
    ( c0 word wtr cep wwe wa tr0 we0 pm3.2i df-ord mpbir ) ABACZADEZFLMGDHIAJK
    $.
    $( [11-May-94] $)

  $( The empty set is an ordinal number.  Corollary 7N(b) of [Enderton]
     p. 193. $)
  0elon $p |- (/) e. On $=
    ( c0 con0 wcel word ord0 0ex elon mpbir ) ABCADEAFGH $.
    $( [17-Sep-93] $)

  $( A non-empty ordinal contains the empty set. $)
  ord0eln0 $p |- ( Ord A -> ( (/) e. A <-> -. A = (/) ) ) $=
    ( word c0 wcel wceq wn wi noel2 a1i wo ord0 wa noel ordtri2 bicon2d mpbiri
    mpan2 ord impbid ) ABZCADZACEZFZUAUCGTACHITUBUATCBZUBUAJZKTUDLZUEACDZFAMUFU
    GUEACNOPQRS $.
    $( [25-Nov-95] $)

  $( The intersection of the class of ordinals is the empty set. $)
  inton $p |- |^| On = (/) $=
    ( c0 con0 wcel cint wceq 0elon int0el ax-mp ) ABCBDAEFBGH $.
    $( [20-Oct-03] $) $( [20-Oct-03] $)

  $( The empty set is not a limit ordinal. $)
  nlim0 $p |- -. Lim (/) $=
    ( c0 wlim wceq cleqid word wn cuni w3a df-lim 3simp2 sylbi mt2 ) ABZAACZADM
    AEZNFZAAGCZHPAIOPQJKL $.
    $( [24-Mar-95] $)

  $( A limit ordinal is ordinal. $)
  limord $p |- ( Lim A -> Ord A ) $=
    ( wlim word c0 wceq wn cuni w3a df-lim 3simp1 sylbi ) ABACZADEFZAAGEZHLAILM
    NJK $.
    $( [4-May-95] $)

  $( A limit ordinal is its own supremum (union). $)
  limuni $p |- ( Lim A -> A = U. A ) $=
    ( wlim word c0 wceq wn cuni w3a df-lim 3simp3 sylbi ) ABACZADEFZAAGEZHNAILM
    NJK $.
    $( [4-May-95] $)

  $( A limit ordinal contains the empty set. $)
  0ellim $p |- ( Lim A -> (/) e. A ) $=
    ( wlim c0 wcel wceq wn nlim0 limeq mtbiri con2i word wb limord ord0eln0
    syl mpbird ) ABZCADZACEZFZSQSQCBGACHIJQAKRTLAMANOP $.
    $( [15-May-94] $)

  $( A limit ordinal class that is also a set is an ordinal number. $)
  limelon $p |- ( ( A e. B /\ Lim A ) -> A e. On ) $=
    ( wcel wlim con0 word elong limord syl5bir imp ) ABCZADZAECZKMAFLABGAHIJ $.
    $( [27-Apr-04] $) $( [26-Apr-04] $)

  $( The class of all ordinals in not empty. $)
  onne0 $p |- -. On = (/) $=
    ( c0 con0 wcel wceq wn 0elon noel2 ax-mp ) ABCBADEFBAGH $.
    $( [17-Sep-95] $)

  $( Equality of successors. $)
  suceq $p |- ( A = B -> suc A = suc B ) $=
    ( wceq csn cun csuc sneq uneq2d uneq1 eqtrd df-suc 3eqtr4g ) ABCZAADZEZBBDZ
    EZAFBFMOAPEQMNPAABGHABPIJAKBKL $.
    $( [30-Aug-93] $)

  $( Membership in a successor.  This one-way implication does not require that
     either ` A ` or ` B ` be sets. $)
  elsuci $p |- ( A e. suc B -> ( A e. B \/ A = B ) ) $=
    ( csuc wcel csn wo wceq cun df-suc eleq2i elun bitr elsni imlor sylbi ) ABC
    ZDZABDZABEZDZFZRABGZFQABSHZDUAPUCABIJABSKLTUBRABMNO $.
    $( [6-Jun-94] $)

  $( Membership in a successor.  Exercise 5 of [TakeutiZaring] p. 17. $)
  elsucg $p |- ( A e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
    ( wcel csn wo wceq csuc elsncg bilord cun df-suc eleq2i elun bitr syl5bb )
    ACDZABDZABEZDZFZRABGZFABHZDZQTUBRABCIJUDABSKZDUAUCUEABLMABSNOP $.
    $( [15-Sep-95] $)

  $( Variant of membership in a successor, requiring that ` B ` rather than
     ` A ` be a set. $)
  elsuc2g $p |- ( B e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
    ( wcel csn cun wceq wo csuc elsnc2g bilord elun syl5bb df-suc eleq2i ) BCDZ
    ABBEZFZDZABDZABGZHZABIZDPTAQDZHUBSPUDUATABCJKABQLMUCRABNOM $.
    $( [28-Oct-03] $) $( [28-Oct-03] $)

  ${
    elsuc.1 $e |- A e. V $.
    $( Membership in a successor.  Exercise 5 of [TakeutiZaring] p. 17. $)
    elsuc $p |- ( A e. suc B <-> ( A e. B \/ A = B ) ) $=
      ( cvv wcel csuc wceq wo wb elsucg ax-mp ) ADEABFEABEABGHICABDJK $.
      $( [15-Sep-03] $) $( [15-Sep-03] $)

    $( Membership in a successor. $)
    elsuc2 $p |- ( B e. suc A <-> ( B e. A \/ B = A ) ) $=
      ( cvv wcel csuc wceq wo wb elsuc2g ax-mp ) ADEBAFEBAEBAGHICBADJK $.
      $( [15-Sep-03] $) $( [15-Sep-03] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbsuc.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for successor. $)
    hbsuc $p |- ( y e. suc A -> A. x y e. suc A ) $=
      ( cv wcel wceq wo wal csuc hbeleq hbor visset elsuc bial 3imtr4 ) BEZCFZQ
      CGZHZTAIQCJFZUAAIRSADABCDKLQCBMNZUATAUBOP $.
      $( [15-Sep-03] $) $( [15-Sep-03] $)
  $}

  $( Membership in a successor. $)
  elelsuc $p |- ( A e. B -> A e. suc B ) $=
    ( wcel csuc wceq wo orc elsucg mpbird ) ABCZABDCJABEZFJKGABBHI $.
    $( [20-Jun-98] $)

  ${
    $d x y A $.  $d x y B $.
    $( Membership of a successor in another class. $)
    sucel $p |- ( suc A e. B <->
                E. x e. B A. y ( y e. x <-> ( y e. A \/ y = A ) ) ) $=
      ( csuc wcel cv wceq wrex wel wo wb wal risset dfcleq visset elsuc bilbi
      bial bitr birex ) CEZDFAGZUBHZADIBAJZBGZCFUFCHKZLZBMZADIAUBDNUDUIADUDUEUF
      UBFZLZBMUIBUCUBOUKUHBUJUGUEUFCBPQRSTUAT $.
      $( [30-Jun-04] $) $( [29-Jun-04] $)
  $}

  $( A proper class is its own successor. $)
  sucprc $p |- ( -. A e. V -> suc A = A ) $=
    ( cvv wcel wn csuc c0 cun csn wceq snprc uneq2 sylbi df-suc syl5eq un0
    syl6eq ) ABCDZAEZAFGZAQAAHZGZSRQTFIUASIAJTFAKLAMNAOP $.
    $( [3-Apr-95] $)

  $( The class of all ordinals is its own successor. $)
  sucon $p |- suc On = On $=
    ( con0 cvv wcel wn csuc wceq onprc sucprc ax-mp ) ABCDAEAFGAHI $.
    $( [12-Sep-03] $)

  ${
    unisuc.1 $e |- A e. V $.
    $( A transitive class is equal to the union of its successor.  Combines
       Theorem 4E of [Enderton] p. 72 and Exercise 6 of [Enderton] p. 73. $)
    unisuc $p |- ( Tr A <-> A = U. suc A ) $=
      ( cuni wss cun wceq wtr csuc ssequn1OLD df-tr csn df-suc unieqi uniun
      unisn uneq2i 3eqtr cleq2i 3bitr4 ) ACZADATAEZFAGAAHZCZFTAIAJUCUAAUCAAKZEZ
      CTUDCZEUAUBUEALMAUDNUFATABOPQRS $.
      $( [30-Aug-93] $)
  $}

  $( A class is included in its own successor.  Part of Proposition 7.23 of
     [TakeutiZaring] p. 41 (generalized to arbitrary classes). $)
  sssucid $p |- A (_ suc A $=
    ( csn cun csuc ssun1 df-suc sseqtr4 ) AAABZCADAHEAFG $.
    $( [31-May-94] $)

  $( A successor exists iff its class argument exists. $)
  sucexb $p |- ( A e. V <-> suc A e. V ) $=
    ( cvv wcel csn wa cun csuc unexb snex biantru df-suc eleq1i 3bitr4 ) ABCZAD
    ZBCZEAOFZBCNAGZBCAOHPNAIJRQBAKLM $.
    $( [22-Jun-98] $)

  $( The successor of a set is a set (generalization). $)
  sucexg $p |- ( A e. B -> suc A e. V ) $=
    ( wcel cvv csuc elisset sucexb sylib ) ABCADCAEDCABFAGH $.
    $( [5-Jun-94] $)

  ${
    sucex.1 $e |- A e. V $.
    $( The successor of a set is a set. $)
    sucex $p |- suc A e. V $=
      ( cvv wcel csuc sucexg ax-mp ) ACDAECDBACFG $.
      $( [30-Aug-93] $)
  $}

  ${
    sucid.1 $e |- A e. V $.
    $( A set belongs to its successor. $)
    sucid $p |- A e. suc A $=
      ( csn wcel csuc snid wo cun df-suc eleq2i elun bitr biimpr olci ax-mp )
      AACZDZAAEZDZABFAADZQSSTQGZSAAPHZDUARUBAAIJAAPKLMNO $.
      $( [22-Jun-94] $)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Part of Proposition 7.23 of [TakeutiZaring] p. 41 (generalized). $)
    sucidg $p |- ( A e. B -> A e. suc A ) $=
      ( vx cv csuc wcel wceq id suceq eleq12d visset sucid vtoclg ) CDZNEZFAAEZ
      FCABNAGZNAOPQHNAIJNCKLM $.
      $( [25-Mar-95] $)
  $}

  $( No successor is empty. $)
  nsuceq0 $p |- -. suc A = (/) $=
    ( cvv wcel csuc c0 wceq wn noel eleq2 sucidg syl5bi com12 mtoi sucprc
    cleq1d 0ex eleq1 mpbiri syl6bi con3d pm2.43i pm2.61i ) ABCZADZEFZGZUCUEAECZ
    AHUEUCUGUEAUDCUGUCUDEAIABJKLMUCGZUFUHUEUCUHUEAEFZUCUHUDAEANOUIUCEBCPAEBQRST
    UAUB $.
    $( [3-Apr-95] $)

  ${
    eqelsuc.1 $e |- A e. V $.
    $( A set belongs to the successor of an equal set. $)
    eqelsuc $p |- ( A = B -> A e. suc B ) $=
      ( wceq csuc wcel sucid suceq eleq2d mpbii ) ABDZAAEZFABEZFACGKLMAABHIJ $.
      $( [18-Aug-94] $)
  $}

  $( A set whose sucessor belongs to a transitive class also belongs. $)
  trsuc $p |- ( ( Tr A /\ suc B e. A ) -> B e. A ) $=
    ( wtr csuc wcel cvv wi trel exp3a sucidg syl5 com12 wn sucprc eleq1d
    biimpd a1d pm2.61i imp ) ACZBDZAEZBAEZBFEZTUBUCGZGTUDUETBUAEZUEUDTUFUBUCABU
    AHIBFJKLUDMZUETUGUBUCUGUABABNOPQRS $.
    $( [5-Sep-03] $)

  $( A member of the successor of a transitive class is a subclass of it. $)
  trsucss $p |- ( Tr A -> ( B e. suc A -> B (_ A ) ) $=
    ( wtr wcel wceq wo wss csuc trss wi eqimss a1i jaod elsuci syl5 ) ACZBADZBA
    EZFBAGZBAHDPQSRABIRSJPBAKLMBANO $.
    $( [4-Oct-03] $) $( [4-Oct-03] $)

  $( A  subset of an ordinal is a member of its successor. $)
  ordsssuc $p |- ( ( A e. On /\ Ord B ) -> ( A (_ B <-> A e. suc B ) ) $=
    ( con0 wcel word wa wss wceq wo csuc wb ordsseleq eloni sylan elsucg
    adantr bitr4d ) ACDZBEZFABGZABDABHIZABJDZAESTUAKRABLAMNRUBUAKSABCOPQ $.
    $( [30-Nov-03] $) $( [28-Nov-03] $)

  $( A  subset of an ordinal number is a member of its successor. $)
  onsssuc $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B <-> A e. suc B ) ) $=
    ( con0 wcel word wss csuc wb ordsssuc eloni sylan5 ) ACDBEABFABGDHBCDABIBJK
    $.
    $( [15-Sep-95] $)

  ${
    $d x A $.  $d x B $.
    $( When its successor is subtracted from a class of ordinal numbers, an
       ordinal number is less than the minimum of the resulting subclass. $)
    onmindif $p |- ( ( A (_ On /\ B e. On ) -> B e. |^| ( A \ suc B ) ) $=
      ( vx con0 wss wcel wa csuc cdif cint cv wral wn wi wb ontri1 onsssuc
      bitr3d bicon1d ssel2 sylan biimpd exp31 com23 imp4b eldif syl5ib
      r19.21aiv elintg adantl mpbird ) ADEZBDFZGZBABHZIZJFZBCKZFZCUPLZUNUSCUPUN
      URAFZURUOFZMZGUSURUPFULUMVAVCUSULVAUMVCUSNZULVAUMVDULVAGZUMGVCUSURDFZUMVC
      USOVEVFUMGZUSVBVGURBEUSMVBURBPURBQRSADURTUAUBUCUDUEURAUOUFUGUHUMUQUTOULCB
      UPDUIUJUK $.
      $( [7-Dec-03] $) $( [1-Dec-03] $)

    $( The minimum of a class of ordinal numbers is less than the minimum
       of that class with its minimum removed. $)
    onmindif2 $p |- ( ( A (_ On /\ -. A = (/) ) ->
                   |^| A e. |^| ( A \ { |^| A } ) ) $=
      ( vx con0 wss c0 wceq wn wa cint csn cdif wcel cv wral wi wo onnmin
      adantlr wb ontri1 onsseleq bitr3d oninton adantr ssel2 sylan13 mpbid ord
      elsn cleqcom bitr syl6ibr con1d exp imp3a eldif syl5ib r19.21aiv cvv
      intex elintg sylbi adantl mpbird ) ACDZAEFGZHZAIZAVHJZKZILZVHBMZLZBVJNZVG
      VMBVJVGVLALZVLVILZGZHVMVLVJLVGVOVQVMVGVOVQVMOVGVOHZVMVPVRVMGVHVLFZVPVRVMV
      SVRVLVHLGZVMVSPZVEVOVTVFAVLQRVHCLZVLCLZVTWASVRWBWCHVHVLDVTWAVHVLTVHVLUAUB
      VGWBVOAUCUDVEVOWCVFACVLUERUFUGUHVPVLVHFVSBVHUIVLVHUJUKULUMUNUOVLAVIUPUQUR
      VFVKVNSZVEVFVHUSLWDAUTBVHVJUSVAVBVCVD $.
      $( [21-Nov-03] $) $( [20-Nov-03] $)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The successor of an ordinal number is an ordinal number.  Proposition
       7.24 of [TakeutiZaring] p. 41. $)
    suceloni $p |- ( A e. On -> suc A e. On ) $=
      ( vx con0 wcel csuc word wtr wss ordon trssord 3exp mpii cv wral csn wo
      onelsst wi wceq elsn eqimss sylbi a1i im2ord cun df-suc eleq2i elun
      bitr2 oridm 3imtr3g sssucid sstr mpi syl6 r19.21aiv dftr3 sylibr wa
      onsst snssi jca unss sylib syl5ss syl13 cvv wb sucexg elong syl mpbird )
      ACDZAEZCDZVNFZVNGZVNCHZVPVMVQVRCFZVPIVQVRVSVPVNCJKLVMBMZVNHZBVNNVQVMWABVN
      VMVTVNDZVTAHZWAVMVTADZVTAOZDZPZWCWCPWBWCVMWDWCWFWCAVTQWFWCRVMWFVTASWCBATV
      TAUAUBUCUDWBVTAWEUEZDWGVNWHVTAUFZUGVTAWEUHUIWCUJUKWCAVNHWAAULVTAVNUMUNUOU
      PBVNUQURVMWHCVNVMACHZWECHZUSWHCHVMWJWKAUTACVAVBAWECVCVDWIVEVFVMVNVGDVOVPV
      HACVIVNVGVJVKVL $.
      $( [6-Jun-94] $)
  $}

  $( There is no set between an ordinal class and its successor.  Generalized
     Proposition 7.25 of [TakeutiZaring] p. 41. $)
  ordnbtwn $p |- ( Ord A -> -. ( A e. B /\ B e. suc A ) ) $=
    ( word wcel wa wo csuc wn ordn2lp ordeirr jca ioran sylibr wceq elsuci
    imlan andi sylib eleq2 biimpac imlor syl nsyl ) ACZABDZBADZEZAADZFZUEBAGDZE
    ZUDUGHZUHHZEUIHUDULUMABIAJKUGUHLMUKUGUEBANZEZFZUIUKUEUFUNFZEUPUJUQUEBAOPUEU
    FUNQRUOUHUGUNUEUHBAASTUAUBUC $.
    $( [21-Jun-98] $)

  $( There is no set between an ordinal number and its successor.  Proposition
     7.25 of [TakeutiZaring] p. 41. $)
  onnbtwn $p |- ( A e. On -> -. ( A e. B /\ B e. suc A ) ) $=
    ( con0 wcel word csuc wa wn eloni ordnbtwn syl ) ACDAEABDBAFDGHAIABJK $.
    $( [9-Jun-94] $)

  $( The successor of an ordinal class is ordinal. $)
  ordsuc $p |- ( Ord A <-> Ord suc A ) $=
    ( cvv wcel word csuc wb con0 elong suceloni eloni syl syl6bir ordelord exp
    sucidg syl5 com12 impbid wn wceq sucprc cleqcomd ordeq pm2.61i ) ABCZADZAEZ
    DZFZUEUFUHUEUFAGCZUHABHUJUGGCUHAIUGJKLUHUEUFUHAUGCZUFUEUHUKUFUGAMNABOPQRUES
    ZAUGTUIULUGAAUAUBAUGUCKUD $.
    $( [3-Apr-95] $)

  $( The successor of an ordinal number is an ordinal number. $)
  sucelon $p |- ( A e. On <-> suc A e. On ) $=
    ( word cvv wcel wa csuc con0 ordsuc sucexb bi2an elon2 3bitr4 ) ABZACDZEAFZ
    BZOCDZEAGDOGDMPNQAHAIJAKOKL $.
    $( [9-Sep-03] $)

  $( The successor of an element of an ordinal class is a subset of it. $)
  ordsucss $p |- ( Ord B -> ( A e. B -> suc A (_ B ) ) $=
    ( word wcel csuc wss wi wa wn ordnbtwn imnan sylibr adantr wb ordtri1
    ordsuc sylanb sylibrd ordelord sylan exp31 pm2.43b ) BCZABDZAEZBFZUDUCUDUFG
    ZUCUDUCUGACZUCUGUCUDHUHUCHUDBUEDZIZUFUHUDUJGZUCUHUDUIHIUKABJUDUIKLMUECUCUFU
    JNUHUEBOAPQRBASTUAUBUB $.
    $( [21-Jun-98] $)

  $( A set whose successor is a subset of another class is a member of that
     class. $)
  sucssel $p |- ( A e. C -> ( suc A (_ B -> A e. B ) ) $=
    ( csuc wss wcel ssel sucidg syl5 com12 ) ADZBEZACFZABFZLAKFNMKBAGACHIJ $.
    $( [16-Sep-95] $)

  $( A set belongs to an ordinal iff its successor is a subset of the
     ordinal.  Exercise 8 of [TakeutiZaring] p. 42 and its converse. $)
  ordelsuc $p |- ( ( A e. C /\ Ord B ) -> ( A e. B <-> suc A (_ B ) ) $=
    ( wcel word wa csuc wss wi ordsucss adantl sucssel adantr impbid ) ACDZBEZF
    ABDZAGBHZPQRIOABJKORQIPABCLMN $.
    $( [29-Nov-03] $) $( [29-Nov-03] $)

  ${
    $d x A $.
    $( The successor of an ordinal number is the smallest larger ordinal
       number. $)
    onsucmin $p |- ( A e. On -> suc A = |^| { x e. On | A e. x } ) $=
      ( con0 wcel csuc cv wss crab cint wceq sucelon intmin sylbi wb word
      ordelsuc eloni sylan5 exp birabdv inteqd eqtr4d ) BCDZBEZUDAFZGZACHZIZBUE
      DZACHZIUCUDCDUDUHJBKAUDCLMUCUJUGUCUIUFACUCUECDZUIUFNZUCUEOULUKBUECPUEQRST
      UAUB $.
      $( [8-Dec-03] $) $( [28-Nov-03] $)
  $}

  $( Membership is inherited by successors.  Generalization of Exercise
     9 of [TakeutiZaring] p. 42. $)
  ordsucelsuc $p |- ( Ord B -> ( A e. B <-> suc A e. suc B ) ) $=
    ( word wcel csuc wi wa cvv wb wss wceq wo ordsseleq ordsuc sylanb adantl
    ordsucss ad2antrr sucssel adantr impbid sucexb elsucg sylbi 3bitr4d exp wn
    elisset sylibr pm5.21ni a1d pm2.61i biimpd ordelord sylan exp31 pm2.43a
    pm2.43d biimprd ) BCZABDZAEZBEZDZUTVAVDVAUTVAVDFZUTVAUTVEACZUTVEUTVAGVFUTGZ
    VAVDAHDZVGVAVDIZFVHVGVIVHVGGZVBBJZVBBDVBBKLZVAVDVGVKVLIZVHVBCZUTVMVFVBBMANZ
    OPVJVAVKUTVAVKFVHVFABQRVHVKVAFVGABHSTUAVHVDVLIZVGVHVBHDZVPAUBZVBBHUCUDTUEUF
    VHUGVIVGVAVHVDABUHVDVQVHVBVCUHVRUIUJUKULZUMBAUNUOUPUQURUTVDVAVDUTVDVAFZUTVD
    UTVTVFUTVTUTVDGVGVAVDVSUSVCCZVDVFUTWAVDGVNVFVCVBUNVOUIBNOUOUPUQURUA $.
    $( [22-Jun-98] $)

  $( The subclass relationship between two ordinal classes is inherited by
     their successors. $)
  ordsucsssuc $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> suc A (_ suc B ) ) $=
    ( word wa wcel wn csuc wss wb ordsucelsuc binegd adantr ordtri1 ordsuc
    syl2anb 3bitr4d ) ACZBCZDBAEZFZBGZAGZEZFZABHUBUAHZQTUDIRQSUCBAJKLABMUBCUACU
    EUDIQRUBUAMANBNOP $.
    $( [4-Oct-03] $) $( [4-Oct-03] $)

  $( Ordinal derived from its successor. $)
  orddif $p |- ( Ord A -> A = ( suc A \ { A } ) ) $=
    ( word csn cin c0 wceq csuc cdif orddisj disj3 cun df-suc difeq1i difun2
    eqtr cleq2i bitr4 sylib ) ABAACZDEFZAAGZSHZFZAITAASHZFUCASJUBUDAUBASKZSHUDU
    AUESALMASNOPQR $.
    $( [20-May-98] $)

  $( An ordinal class includes its union. $)
  orduniss $p |- ( Ord A -> U. A (_ A ) $=
    ( word wtr cuni wss ordtr df-tr sylib ) ABACADAEAFAGH $.
    $( [13-Sep-03] $)

  $( A trichotomy law for ordinal classes. $)
  ordtri2or $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ B (_ A ) ) $=
    ( word wa wcel wss wo wceq w3o ordtri3or 3orass sylib wb ordsseleq ancoms
    orcom cleqcom biror bitr syl6bb bilord mpbird ) ACZBCZDZABEZBAFZGUFABHZBAEZ
    GZGZUEUFUHUIIUKABJUFUHUIKLUEUGUJUFUEUGUIBAHZGZUJUDUCUGUMMBANOUMULUIGUJUIULP
    ULUHUIBAQRSTUAUB $.
    $( [13-Sep-03] $)

  $( A trichotomy law for ordinal classes. $)
  ordtri2or2 $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B \/ B (_ A ) ) $=
    ( word wa wcel wss wo ordtri2or wi ordelss exp imrord adantl mpd ) ACZBCZDA
    BEZBAFZGZABFZRGZABHPSUAIOPQTRPQTBAJKLMN $.
    $( [11-Dec-03] $) $( [2-Nov-03] $)

  $( Property of a subclass of the maximum (i.e. union) of two ordinals. $)
  ordssun $p |- ( ( Ord B /\ Ord C ) ->
               ( A (_ ( B u. C ) <-> ( A (_ B \/ A (_ C ) ) ) $=
    ( word wa cun wss wo wi ordtri2or2 wceq wb ssequn1OLD sseq2 sylbi olc
    syl6bir ssequn2 orc jaoi syl ssun a1i impbid ) BDCDEZABCFZGZABGZACGZHZUEBCG
    ZCBGZHUGUJIZBCJUKUMULUKUGUIUJUKCUFKUIUGLBCMCUFANOUIUHPQULUGUHUJULBUFKUHUGLC
    BRBUFANOUHUISQTUAUJUGIUEABCUBUCUD $.
    $( [23-Dec-03] $) $( [28-Nov-03] $)

  $( The maximum (i.e. union) of two ordinals is either one or the other.
     Similar to Exercise 14 of [TakeutiZaring] p. 40. $)
  ordequn $p |- ( ( Ord B /\ Ord C ) ->
               ( A = ( B u. C ) -> ( A = B \/ A = C ) ) ) $=
    ( word wa wss wo cun wceq wi ordtri2or2 wb ssequn1OLD cleq2 sylbi olc
    syl6bir ssequn2 orc jaoi syl ) BDCDEBCFZCBFZGABCHZIZABIZACIZGZJZBCKUBUIUCUB
    UEUGUHUBCUDIUGUELBCMCUDANOUGUFPQUCUEUFUHUCBUDIUFUELCBRBUDANOUFUGSQTUA $.
    $( [26-Dec-03] $) $( [28-Nov-03] $)

  $( The maximum (i.e. union) of two ordinals is ordinal.  Exercise 12 of
     [TakeutiZaring] p. 40. $)
  ordun $p |- ( ( Ord A /\ Ord B ) -> Ord ( A u. B ) ) $=
    ( word wa cun wceq wo cleqid ordequn mpi ordeq biimprcd jaao mpd ) ACZBCZDZ
    ABEZAFZRBFZGZRCZQRRFUARHRABIJOSUBPTSUBORAKLTUBPRBKLMN $.
    $( [29-Dec-03] $) $( [28-Nov-03] $)

  ${
    $d x A $.  $d x B $.
    $( The successor of the maximum (i.e. union) of two ordinals is the
       maximum of their successors. $)
    ordsucun $p |- ( ( Ord A /\ Ord B ) ->
               suc ( A u. B ) = ( suc A u. suc B ) ) $=
      ( vx word wa cun csuc cv con0 wcel wb wo wss ordssun adantl ordsssuc
      ordun sylan5 adantrr adantrl bi2ord 3bitr3d elun syl6bbr exp com12
      pm5.32d wi ordsuc ordelon sylbi syl pm4.71r sylib syl2anb 3bitr4d cleqrd
      ) ADZBDZEZCABFZGZAGZBGZFZUTCHZIJZVFVBJZEZVGVFVEJZEZVHVJUTVGVHVJVGUTVHVJKZ
      VGUTVLVGUTEZVHVFVCJZVFVDJZLZVJVMVFVAMZVFAMZVFBMZLZVHVPUTVQVTKVGVFABNOVGVA
      DZVQVHKUTVFVAPABQZRVMVRVNVSVOVGURVRVNKUSVFAPSVGUSVSVOKURVFBPTUAUBVFVCVDUC
      UDUEUFUGUTVHVGUHZVHVIKUTWAWCWBWAVBDZWCVAUIWDVHVGVBVFUJUEUKULVHVGUMUNUTVJV
      GUHZVJVKKVCDZVDDZWEURUSWFWGEVEDZWEVCVDQWHVJVGVEVFUJUEULAUIBUIUOVJVGUMUNUP
      UQ $.
      $( [7-Feb-04] $) $( [28-Nov-03] $)

    $( A subclass relationship for union and successor of ordinal classes. $)
    ordunisuc $p |- ( ( A (_ On /\ Ord B ) -> ( U. A (_ B <-> A (_ suc B ) ) $=
      ( vx con0 wss word wa cv wral csuc wcel cuni wb ordsssuc ssel2 sylan
      an1rs biraldva unissb dfss3 3bitr4g ) ADEZBFZGZCHZBEZCAIUEBJZKZCAIALBEAUG
      EUDUFUHCAUBUEAKZUCUFUHMZUEDKUCUJUBUIGUEBNADUEOPQRCABSCAUGTUA $.
      $( [3-Dec-03] $) $( [28-Nov-03] $)
  $}

  $( A class of ordinal numbers is a subclass of the successor of its
     union.  Similar to Proposition 7.26 of [TakeutiZaring] p. 41. $)
  onsucuni $p |- ( A (_ On -> A (_ suc U. A ) $=
    ( con0 wss cuni word csuc ssorduni wa ssid ordunisuc mpbii mpand ) ABCZADZE
    ZANFCZAGMOHNNCPNIANJKL $.
    $( [19-Sep-03] $)

  $( An ordinal class is a subclass of the successor of its union. $)
  ordsucuni $p |- ( Ord A -> A (_ suc U. A ) $=
    ( word con0 wss cuni csuc ordsson onsucuni syl ) ABACDAAEFDAGAHI $.
    $( [12-Sep-03] $)

  $( An ordinal class is either its union or the successor of its union. $)
  orduniorsuc $p |- ( Ord A -> ( A = U. A \/ A = suc U. A ) ) $=
    ( word cuni wceq csuc wn wss wa ordsucuni a1d wcel wi orduniss wb
    ordelssne uniord sylan anidms biimprd exp3a mpd ordsucss syld jcad cleqcom
    bineg eqss 3imtr4g orrd ) ABZAACZDZAUKEZDZUJUKADZFZAUMGZUMAGZHULFUNUJUPUQUR
    UJUQUPAIJUJUPUKAKZURUJUKAGZUPUSLAMUJUTUPUSUJUSUTUPHZUJUSVANZUKBUJVBUJUKAOAP
    QRSTUAUKAUBUCUDULUOAUKUEUFAUMUGUHUI $.
    $( [13-Sep-03] $)

  ${
    $d x y $.
    $( The class of all ordinals is its own union.  Exercise 11 of
       [TakeutiZaring] p. 40. $)
    unon $p |- U. On = On $=
      ( vx vy con0 cuni cv wcel wel wrex eluni2 onelon exp r19.23aiv sylbi
      csuc wa suceloni visset sucid jctil elunii syl impbi cleqri ) ACDZCAEZUDF
      ZUECFZUFABGZBCHUGBUECIUHUGBCBEZCFUHUGUIUEJKLMUGUEUENZFZUJCFZOUFUGULUKUEPU
      EAQRSUEUJCTUAUBUC $.
      $( [12-Nov-03] $) $( [12-Nov-03] $)
  $}

  $( The successor of an ordinal class contains the empty set. $)
  0elsuc $p |- ( Ord A -> (/) e. suc A ) $=
    ( word csuc c0 wcel ordsuc wceq wn nsuceq0 ord0eln0 mpbiri sylbi ) ABACZBZD
    MEZAFNOMDGHAIMJKL $.
    $( [4-Apr-95] $)

  $( The successor operation behaves like a one-to-one function.  Compare
     Exercise 16 of [Enderton] p. 194. $)
  suc11 $p |- ( ( A e. On /\ B e. On ) -> ( suc A = suc B <-> A = B ) ) $=
    ( con0 wcel wa csuc wceq wn wo wi word eloni ordn2lp ianor sylib syl
    adantr wss sucssel eqimss syl5 elsuci ord com12 syl9 eqimss2 cleqcom
    syl6ib jaao mpd suceq a1i impbid ) ACDZBCDZEZAFZBFZGZABGZUPABDZHZBADZHZIZUS
    UTJZUNVEUOUNAKZVEALVGVAVCEHVEABMVAVCNOPQUNVBVFUOVDUNUSAURDZVBUTUNUQURRVHUSA
    URCSUQURTUAVHVBUTVHVAUTABUBUCUDUEUOUSBUQDZVDUTUOURUQRVIUSBUQCSURUQUFUAVDVIB
    AGZUTVIVDVJVIVCVJBAUBUCUDBAUGUHUEUIUJUTUSJUPABUKULUM $.
    $( [3-Sep-03] $)

  $( The class of ordinal numbers is a limit ordinal. $)
  limon $p |- Lim On $=
    ( con0 wlim word c0 wceq wn cuni w3a ordon onne0 unon cleqcomi 3pm3.2i
    df-lim mpbir ) ABACZADEFZAAGZEZHPQSIJRAKLMANO $.
    $( [24-Mar-95] $)

  ${
    on.1 $e |- A e. On $.
    $( An ordinal number is an ordinal class. $)
    onord $p |- Ord A $=
      ( con0 wcel word eloni ax-mp ) ACDAEBAFG $.
      $( [11-Jun-94] $)

    $( An ordinal number is a transitive class. $)
    ontrc $p |- Tr A $=
      ( word wtr onord ordtr ax-mp ) ACADABEAFG $.
      $( [11-Jun-94] $)

    $( An ordinal number is not a member of itself.  Theorem 7M(c) of
       [Enderton] p. 192. $)
    oneirr $p |- -. A e. A $=
      ( word wcel wn onord ordeirr ax-mp ) ACAADEABFAGH $.
      $( [11-Jun-94] $)

    $( A member of an ordinal number is an ordinal number.  Theorem 7M(a) of
       [Enderton] p. 192. $)
    onel $p |- ( B e. A -> B e. On ) $=
      ( con0 wcel onelon mpan ) ADEBAEBDECABFG $.
      $( [11-Jun-94] $)

    $( An ordinal number is a subset of ` On ` . $)
    onss $p |- A (_ On $=
      ( con0 wcel wss onsst ax-mp ) ACDACEBAFG $.
      $( [11-Aug-94] $)

    $( A member of an ordinal number is a subset of it. $)
    onelss $p |- ( B e. A -> B (_ A ) $=
      ( con0 wcel wss wi onelsst ax-mp ) ADEBAEBAFGCABHI $.
      $( [11-Aug-94] $)

    $( An ordering law for ordinals. $)
    onssneli $p |- ( A (_ B -> -. B e. A ) $=
      ( wcel wss con0 word wn onel eloni ordeirr 3syl ssel com12 mtod con2i )
      BADZABEZQRBBDZQBFDBGSHABCIBJBKLRQSABBMNOP $.
      $( [13-Jun-94] $)

    $( An ordering law for ordinals. $)
    onssneli2 $p |- ( B (_ A -> -. A e. B ) $=
      ( wss wcel oneirr ssel mtoi ) BADABEAAEACFBAAGH $.
      $( [13-Jun-94] $)

    $( An element of an ordinal number equals the intersection with it. $)
    onelin $p |- ( B e. A -> B = ( B i^i A ) ) $=
      ( wcel wss cin wceq onelss dfss sylib ) BADBAEBBAFGABCHBAIJ $.
      $( [11-Jun-94] $)

    $( An ordinal number equals its union with any element. $)
    onelun $p |- ( B e. A -> A = ( A u. B ) ) $=
      ( wcel wss cun wceq onelss ssequn2 sylib ) BADBAEAABFGABCHBAIJ $.
      $( [13-Jun-94] $)

    $( The successor of an ordinal number is an ordinal number.  Corollary
       7N(c) of [Enderton] p. 193. $)
    onsuc $p |- suc A e. On $=
      ( con0 wcel csuc suceloni ax-mp ) ACDAECDBAFG $.
      $( [12-Jun-94] $)

    $( An ordinal number is equal to the union of its successor. $)
    onunisuc $p |- A = U. suc A $=
      ( wtr csuc cuni wceq ontrc con0 elisseti unisuc mpbi ) ACAADEFABGAAHBIJK
      $.
      $( [12-Jun-94] $)

    $( An ordinal number is either its own union (if zero or a limit
       ordinal) or the successor of its union. $)
    onuniorsuc $p |- ( A = U. A \/ A = suc U. A ) $=
      ( word cuni wceq csuc wo onord orduniorsuc ax-mp ) ACAADZEAKFEGABHAIJ $.
      $( [13-Jun-94] $)

    ${
      $d x A $.
      $( A limit ordinal is not a successor ordinal. $)
      onuninsuc $p |- ( A = U. A <-> -. E. x e. On A = suc x ) $=
        ( cv csuc wceq con0 wrex cuni wn wi wcel wa oneirr id cun csn df-suc
        cleq2i unieq sylbi uniun visset unisn uneq2i eqtr syl6eq wss eleq1
        mpbii wtr word ordon ordtr ax-mp trsuc mpan eloni syl df-tr sylib 3syl
        ssequn1OLD eqtr4d sylan9eqr sucid eleq2 mpbiri adantr eqeltrd mto
        imnan mpbir a1i r19.23aiv onuniorsuc ori onss elisseti onuni jctil
        suceq cleq2d rcla4ev impbi bicon2 ) BADZEZFZAGHZBBIZFZWJWLJZWIWMAGWIWMK
        ZWGGLZWNWIWLMZJWPBBLBCNWPBWGBWLWIBWKWGWLOWIWKWGIZWGPZWGWIWKWGWGQZPZIZWR
        WIBWTFWKXAFWHWTBWGRSBWTTUAXAWQWSIZPWRWGWSUBXBWGWQWGAUCZUDUEUFUGWIWQWGUH
        ZWGWRFWIWHGLZWOXDWIBGLXECBWHGUIUJGUKZXEWOGULXFUMGUNUOGWGUPUQWOWGUKZXDWO
        WGULXGWGURWGUNUSWGUTVAVBWQWGVCVAVDVEWIWGBLZWLWIXHWGWHLWGXCVFBWHWGVGVHVI
        VJVKWIWLVLVMVNVOWMWKGLZBWKEZFZMWJWMXKXIWLXKBCVPVQBGUHXIBCVRBBGCVSVTUOWA
        WIXKAWKGWGWKFWHXJBWGWKWBWCWDUSWEWF $.
        $( [21-Feb-04] $) $( [18-Feb-04] $)
    $}

    ${
      on.2 $e |- B e. On $.
      $( Subset is equivalent to membership or equality for ordinal numbers. $)
      onssel $p |- ( A (_ B <-> ( A e. B \/ A = B ) ) $=
        ( con0 wcel wss wceq wo wb onsseleq mp2an ) AEFBEFABGABFABHIJCDABKL $.
        $( [15-Sep-95] $)

      $( The union of two ordinal numbers is an ordinal number. $)
      onun $p |- ( A u. B ) e. On $=
        ( wcel wss wo cun con0 word onord ordtri2or mp2an wceq onelun eleq1
        mpbii syl ssequn1OLD sylbi jaoi ax-mp ) BAEZABFZGZABHZIEZBJAJUEBDKACKBA
        LMUCUGUDUCAUFNZUGABCOUHAIEUGCAUFIPQRUDBUFNZUGABSUIBIEUGDBUFIPQTUAUB $.
        $( [13-Jun-94] $)

      $( A set belongs to an ordinal number iff its successor is a subset of
         the ordinal number.  Exercise 8 of [TakeutiZaring] p. 42 and its
         converse. $)
      onsucss $p |- ( A e. B <-> suc A (_ B ) $=
        ( con0 wcel word csuc wss wb onord ordelsuc mp2an ) AEFBGABFAHBIJCBDKAB
        ELM $.
        $( [16-Sep-95] $)

      $( Lemma for ~ limsuc . $)
      limsuclem $p |- ( ( Lim A /\ B e. A ) -> suc B e. A ) $=
        ( wlim wcel csuc wn wi wceq wss wo onsucss onsuc onssel df-or 3bitr
        biimp wa cuni limuni unieq onunisuc syl5req sylan9eqr eqimss onssneli
        3syl exp syl6 com3r ax-3 pm2.43d imp ) AEZBAFZBGZAFZUOUPURUOUPURHZUPHZI
        UPURIUPUSUOUTUPUSUQAJZUOUTIUPUSVAIZUPUQAKURVALVBBADCMUQABDNCOURVAPQRVAU
        OUTVAUOSABJABKUTUOVAAATZBAUAVAUQTVCBUQAUBBDUCUDUEABUFABCUGUHUIUJUKURUPU
        LUJUMUN $.
        $( [14-Jun-94] $)
    $}
  $}

  $( The successor of a member of a limit ordinal is also a member. $)
  limsuc $p |- ( Lim A -> ( B e. A <-> suc B e. A ) ) $=
    ( wlim wcel csuc con0 wceq wo wi word limord ordeleqon sylib wa onelon c0
    cded limeq eleq2 bi2and bi2imd eleq1 biland suceq eleq1d 0elon elimel
    limsuclem dedth2h exp4b com34 com23 imp mpd exp suceloni mpbiri a1d jaoi
    mpcom wtr ordtr trsuc 3syl impbid ) ACZBADZBEZADZAFDZAFGZHZVFVGVIIZVFAJZVLA
    KZALMVJVFVMIVKVJVGVFVIVJVGVFVIIZVJVGNBFDZVPABOVJVGVQVPIVJVQVGVPVJVQVFVGVIVJ
    VQVFVGVIVJVQVFVGNZVIIVJAPQZCZBVSDZNZVHVSDZIVTVQBPQZVSDZNZWDEZVSDZIABPPAVSGZ
    VRWBVIWCWIVFVTVGWAAVSRAVSBSTAVSVHSUABWDGZWBWFWCWHWJWAWEVTBWDVSUBUCWJVHWGVSB
    WDUDUEUAVSWDAPFUFUGBPFUFUGUHUIUJUKULUMUNUOULVKVMVFVKVMVQVHFDZIBUPVKVGVQVIWK
    AFBSAFVHSUAUQURUSUTVFVNAVAZVIVGIVOAVBWLVIVGABVCUOVDVE $.
    $( [3-Sep-03] $)

  ${
    $d x A $.  $d x B $.
    $( A class includes a limit ordinal iff the sucessor of the class includes
       it. $)
    limsssuc $p |- ( Lim A -> ( A (_ B <-> A (_ suc B ) ) $=
      ( vx wlim wss csuc wi sssucid sstr mpi a1i wa cv wcel wceq wn eleq1
      biimpcd limsuc biimpa word wb ordtri1 limord adantr ordelord sylan
      ordsuc sylib sylan13 bicon2d mpbid exp sylan9r con2d com23 imp31 wo
      ssel2 visset elsuc ord con1d adantll mpd ssrdv impbid ) ADZABEZABFZEZVIVK
      GVHVIBVJEVKBHABVJIJKVHVKVIVHVKLZCABVLCMZANZVMBNZVLVNLVMBOZPZVOVHVKVNVQVHV
      NVKVQVHVNVKVQGVHVNLVPVKVNVPBANZVHVKPZVPVNVRVMBAQRVHVRVSVHVRLZVJANZVSVHVRW
      AABSTVTVKWAAUAZVJUAZVKWAPUBVTAVJUCVHWBVRAUDZUEVTBUAZWCWBVRWEVHABUFWDUGBUH
      UIUJUKULUMUNUOUMUPUQVKVNVQVOGVHVKVNLZVOVPWFVOVPWFVMVJNVOVPURAVJVMUSVMBCUT
      VAUIVBVCVDVEUMVFUMVG $.
      $( [6-Nov-03] $) $( [30-Oct-03] $)
  $}

  ${
    nlimsuc.1 $e |- A e. V $.
    $( A successor is not a limit ordinal. $)
    nlimsuc $p |- -. Lim suc A $=
      ( word csuc wlim wn cuni wceq wcel ordsuc ordeirr sylbi eleq1 sucid wtr
      ordtr unisuc sylib eleq1d mpbii syl5bir com12 mtod limuni nsyl c0 w3a
      3simp1 df-lim 3imtr4 con3i pm2.61i ) ACZADZEZFUMUNUNGZHZUOUMUQUNUNIZUMUNC
      ZURFAJZUNKLUQUMURUQURUPUNIZUMUNUPUNMUMAUNIVAABNUMAUPUNUMAOAUPHAPABQRSTUAU
      BUCUNUDUEUOUMUSUNUFHFZUQUGUSUOUMUSVBUQUHUNUIUTUJUKUL $.
      $( [25-Mar-95] $)
  $}

  $( An ordinal equal to its own union is either zero or a limit ordinal. $)
  unizlim $p |- ( Ord A -> ( A = U. A <-> ( A = (/) \/ Lim A ) ) ) $=
    ( word cuni wceq c0 wlim wo wa wn wi w3a df-lim biimpr 3exp com23 imp orrd
    exp uni0 cleqcomi id unieq cleq12d mpbiri limuni jaoi a1i impbid ) ABZAACZD
    ZAEDZAFZGZUIUKUNUIUKHULUMUIUKULIZUMJUIUOUKUMUIUOUKUMUMUIUOUKKALMNOPQRUNUKJU
    IULUKUMULUKEECZDUPESTULAEUJUPULUAAEUBUCUDAUEUFUGUH $.
    $( [1-Oct-03] $) $( [1-Oct-03] $)

  ${
    $d x A $.
    $( A limit ordinal is not a successor ordinal. $)
    orduninsuc $p |- ( Ord A -> ( A = U. A <-> -. E. x e. On A = suc x ) ) $=
      ( word con0 wcel wceq wo cuni cv csuc wrex wn wb ordeleqon c0 cded id
      unieq cleq12d cleq1 birexdv binegd bi2bid 0elon elimel onuninsuc dedth
      unon cleqcomi cvv onprc visset sucex eleq1 mpbiri mto a1i nrex 2th jaoi
      sylbi ) BCBDEZBDFZGBBHZFZBAIZJZFZADKZLZMZBNVBVKVCVBVKVBBOPZVLHZFZVLVGFZAD
      KZLZMBOBVLFZVEVNVJVQVRBVLVDVMVRQBVLRSVRVIVPVRVHVOADBVLVGTUAUBUCAVLBODUDUE
      UFUGVCVKDDHZFZDVGFZADKZLZMVTWCVSDUHUIWAADWALVFDEWADUJEZUKWAWDVGUJEVFAULUM
      DVGUJUNUOUPUQURUSVCVEVTVJWCVCBDVDVSVCQBDRSVCVIWBVCVHWAADBDVGTUAUBUCUOUTVA
      $.
      $( [21-Feb-04] $) $( [18-Feb-04] $)

    $( An ordinal is zero, a successor ordinal, or a limit ordinal. $)
    ordzsl $p |- ( Ord A <->
                  ( A = (/) \/ E. x e. On A = suc x \/ Lim A ) ) $=
      ( word c0 wceq cv csuc con0 wrex wlim w3o wo wn cuni orduninsuc biimprd
      unizlim sylibd orrd 3orass or12 bitr sylibr ord0 ordeq mpbiri wcel eleq1
      suceloni syl5bir com12 eloni syl6 r19.23aiv limord 3jaoi impbi ) BCZBDEZB
      AFZGZEZAHIZBJZKZURVCUSVDLZLZVEURVCVFURVCMZBBNEZVFURVIVHABOPBQRSVEUSVCVDLL
      VGUSVCVDTUSVCVDUAUBUCUSURVCVDUSURDCUDBDUEUFVBURAHUTHUGZVBBHUGZURVBVJVKVBV
      KVAHUGVJBVAHUHUTUIUJUKBULUMUNBUOUPUQ $.
      $( [2-Oct-03] $) $( [1-Oct-03] $)

    $( An ordinal number is zero, a successor ordinal, or a limit ordinal
       number. $)
    onzsl $p |- ( A e. On <->
               ( A = (/) \/ E. x e. On A = suc x \/ ( A e. V /\ Lim A ) ) ) $=
      ( con0 wcel cvv wa word c0 wceq cv csuc wrex wlim w3o elisset pm4.71ri
      elong pm5.32i wo andi 0ex eleq1 mpbiri wi visset sucex a1i r19.23aiv
      jaoi biror bitr4 ordzsl df-3or bitr bilan 3bitr4 3bitr ) BCDZBEDZURFUSBGZ
      FZBHIZBAJZKZIZACLZUSBMZFZNZURUSBCOPUSURUTBEQRUSVBVFSZVGSZFZVJVHSZVAVIVLUS
      VJFZVHSVMUSVJVGTVJVNVHVJUSVBUSVFVBUSHEDUABHEUBUCVEUSACVEUSUDVCCDVEUSVDEDV
      CAUEUFBVDEUBUCUGUHUIPUJUKUTVKUSUTVBVFVGNVKABULVBVFVGUMUNUOVBVFVHUMUPUQ $.
      $( [5-Jan-04] $) $( [1-Oct-03] $)
  $}

  $( An ordinal number either equals zero or contains zero. $)
  on0eqel $p |- ( A e. On -> ( A = (/) \/ (/) e. A ) ) $=
    ( con0 wcel c0 wceq wo wss 0ss wb 0elon onsseleq mpan mpbii cleqcom bilor
    orcom bitr sylib ) ABCZDACZDAEZFZADEZTFZSDAGZUBAHDBCSUEUBIJDAKLMUBTUCFUDUAU
    CTDANOTUCPQR $.
    $( [2-Jun-04] $) $( [1-Jun-04] $)

  $( The singleton of the singleton of the empty set is not an ordinal (nor a
     natural number by ~ omsson ).  It can be used to represent an "undefined"
     value for a partial operation on natural numbers or ordinals.  See also
     ~ onxpdisj . $)
  snsn0non $p |- -. { { (/) } } e. On $=
    ( c0 csn con0 wcel wss wceq 0ex snnz elsnc cleqcom bitr mtbir snid ssel
    mpi mto p0ex onelsst ) ABZBZCDZSTEZUBATDZUCSAFZAGHUCASFUDASGIASJKLUBASDUCAG
    MSTANOPUASTDUBSQMTSROP $.
    $( [21-May-04] $) $( [21-May-04] $)

