$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Ord $. $( Ordinal predicate $)
  $c On $. $( The class of ordinals $)
  $c Lim $. $( Limit ordinal predicate $)
  $c suc $. $( Successor function (read:  'successor of') $)

  $( Extend the definition of a wff to include the ordinal predicate. $)
  word $a wff Ord A $.

  $( Extend the definition of a class to include the class of all ordinals. $)
  con $a class On $.

  $( Extend the definition of a wff to include the limit ordinal predicate. $)
  wlim $a wff Lim A $.

  $( Extend class notation to include the successor function. $)
  csuc $a class suc A $.

  $( Define the ordinal predicate, which is true for a class that is transitive
     and is well-ordered by the epsilon relation.  Similar to definition on
     p. 468, Bell and Machover. $)
  df-ord $a |- ( Ord A <-> ( Tr A /\ E We A ) ) $.

  $( Define the class of all ordinals.  Definition 7.11 of Takeuti and
     Zaring, p. 38. $)
  df-on $a |- On = { x | Ord x } $.

  $( Define the limit ordinal predicate, which is true for a non-empty ordinal
     that is not a successor (i.e. that is the union of itself).  Compare Bell
     and Machover, p. 471 and Exercise (1), p. 42 of Takeuti and Zaring. $)
  df-lim $a |- ( Lim A <-> ( Ord A /\ -. A = (/) /\ A = U. A ) ) $.

  $( Define the successor of a class.  Definition 7.22 of Takeuti and Zaring,
     p. 41.  Our definition is a generalization to classes, although it
     is meaningless when classes are proper.  $)
  df-suc $a |- suc A = ( A u. { A } ) $.

  $( Equality theorem for the ordinal predicate. $)
  ordeq $p |- ( A = B -> ( Ord A <-> Ord B ) ) $=
    ( wceq wtr cep wwe wa word treq weeq2 bi2and df-ord 3bitr4g ) ABCZADZAEFZGB
    DZBEFZGAHBHNOQPRABIABEJKALBLM $.
    $([17-Sep-93]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Membership in the ` On ` class is equivalent to the ordinal predicate
       (generalized). $)
    elong   $p |- ( A e. B -> ( A e. On <-> Ord A ) ) $=
      ( vx cv word con ordeq df-on vtoclabg ) CDZEAECAFBJAGCHI $.
      $([5-Jun-94]$)
  $}

  ${
    elon.1 $e |- A e. V $.
    $( Membership in the ` On ` class is equivalent to the ordinal
       predicate. $)
    elon   $p |- ( A e. On <-> Ord A ) $=
      ( cvv wcel con word wb elong ax-mp ) ACDAEDAFGBACHI $.
      $([5-Jun-94]$)
  $}

  $( Membership in the ` On ` class implies the ordinal predicate. $)
  eloni   $p |- ( A e. On -> Ord A ) $=
    ( con wcel word elong biimpd pm2.43i ) ABCZADZHHIABEFG $.
    $([5-Jun-94]$)

  $( Equality theorem for the limit predicate. $)
  limeq $p |- ( A = B -> ( Lim A <-> Lim B ) ) $=
    ( wceq word c0 wn cuni w3a wlim ordeq cleq1 binegd unieq cleq2d bitrd
    bi3and df-lim 3bitr4g ) ABCZADZAECZFZAAGZCZHBDZBECZFZBBGZCZHAIBISTUEUBUGUDU
    IABJSUAUFABEKLSUDAUHCUISUCUHAABMNABUHKOPAQBQR $.
    $([22-Apr-94]$)

  $( Epsilon well orders every ordinal.  Proposition 7.4, p. 36, of Takeuti
     and Zaring. $)
  ordwe $p |- ( Ord A -> E We A ) $=
    ( word wtr cep wwe df-ord pm3.27bd ) ABACADEAFG $.
    $([3-Apr-94]$)

  $( An ordinal class is transitive. $)
  ordtr $p |- ( Ord A -> Tr A ) $=
    ( word wtr cep wwe df-ord pm3.26bd ) ABACADEAFG $.
    $([3-Apr-94]$)

  $( Epsilon is well-founded on an ordinal class. $)
  ordfr $p |- ( Ord A -> E Fr A ) $=
    ( word cep wwe wfr ordwe wefr syl ) ABACDACEAFACGH $.
    $([22-Apr-94]$)

  $( An element of an ordinal is a subset of it. $)
  ordelss $p |- ( ( Ord A /\ B e. A ) -> B (_ A ) $=
    ( wtr wcel wss word trss imp ordtr sylan ) ACZBADZBAEZAFKLMABGHAIJ $.
    $([30-May-94]$)

  $( A transitive subclass of an ordinal is ordinal. $)
  trssord $p |- ( ( Tr A /\ A (_ B /\ Ord B ) -> Ord A ) $=
    ( wtr wss word wa cep wwe w3a wess imp ordwe sylan5 imlan 3anass df-ord
    3imtr4 ) ACZABDZBEZFZFRAGHZFRSTIAEUAUBRSBGHZUBTSUCUBABGJKBLMNRSTOAPQ $.
    $([29-May-94]$)

  $( No ordinal is a member of itself.  Theorem 2.2(i), p. 469, of Bell and
     Machover, generalized to classes. $)
  ordeirr $p |- ( Ord A -> -. A e. A ) $=
    ( word cep wfr wcel wn ordfr efrirr syl ) ABACDAAEFAGAHI $.
    $([2-Jan-94]$)

  $( A member of an ordinal is not equal to it. $)
  nordeq $p |- ( ( Ord A /\ B e. A ) -> -. A = B ) $=
    ( word wcel wceq wn eleq1 binegd ordeirr syl5bi comm pm2.03d imp ) ACZBADZA
    BEZFNPOPNOFZPAADZFQNPROABAGHAIJKLM $.
    $([25-May-98]$)

  $( An ordinal cannot an element of one of its members.  Similar to first
     part of Theorem 2.2(vii), p. 469, of Bell and Machover. $)
  ordn2lp $p |- ( Ord A -> -. ( A e. B /\ B e. A ) ) $=
    ( word wcel wa ordeirr wtr wi ordtr trel syl mtod ) ACZABDBADEZAADZAFMAGNOH
    AIAABJKL $.
    $([3-Apr-94]$)

  ${
    $d x R $.  $d x A $.  $d x B $.
    $( A subclass (possibly proper) of an ordinal has a minimal element.
       Proposition 7.5 of Takeuti and Zaring, p. 36. $)
    tz7.5 $p |- ( ( Ord A /\ ( B (_ A /\ -. B = (/) ) ) ->
               E. x ( x e. B /\ ( B i^i x ) = (/) ) ) $=
      ( cep wwe wss c0 wceq wn wa cv wcel cin wex word wefrc ordwe sylan ) BDEC
      BFCGHIJAKZCLCSMGHJANBOABCPBQR $.
      $([28-Apr-94]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( An element of an ordinal is ordinal.  Proposition 7.6 of Takeuti and
       Zaring, p. 36. $)
    tz7.6 $p |- ( ( Ord A /\ B e. A ) -> Ord B ) $=
      ( vx vz vy word wcel cv wa wi wceq eleq1 biland ordeq bi2imd wtr cep wwe
      wel wal w3a wetrep ordwe sylan pm3.26 adantr ordtr trel3 syl 3anrot
      3anass bitr3 syl5ibr exp3a imp31 trel com23 adantarl pm3.27 3jca sylan13
      exp pm2.43d 19.21aivv dftr2 sylibr wss trss wess comm syld imp jca
      df-ord vtoclg anabsi7 ) AFZBAGZBFZVQCHZAGZIZVTFZJVQVRIZVSJCBAVTBKZWBWDWCV
      SWEWAVRVQVTBALMVTBNOWBVTPZVTQRZIWCWBWFWGWBDESZECSZIZDCSZJZETDTWFWBWLDEWBW
      JWKWBWJWLVQDHZAGZEHZAGZWAUAZWLWBWJIZAQRZWQWLVQDECAUBAUCZUDWBVQWJVQWAUEUFW
      RWNWPWAVQWAWJWNVQWAWJWNVQWHWIWAUAZWNWAWJIZVQAPZXAWNJAUGZAWMWOVTUHUIXAWAWH
      WIUAXBWAWHWIUJWAWHWIUKULUMUNUOWBWIWPWHVQWAWIWPVQWIWAWPVQWIWAWPVQXCWIWAIWP
      JXDAWOVTUPUIUNUQUOURWBWAWJVQWAUSUFUTVAVBVCVDDEVTVEVFVQWAWGVQWAVTAVGZWGVQX
      CWAXEJXDAVTVHUIVQWSXEWGJWTXEWSWGVTAQVIVJUIVKVLVMVTVNVFVOVP $.
      $([23-Apr-94]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Proposition 7.7 of Takeuti and Zaring, p. 37. $)
    tz7.7 $p |- ( ( Ord A /\ Tr B ) ->
                ( ( B (_ A /\ -. B = A ) <-> B e. A ) ) $=
      ( vx vy word wtr wa wss wceq wn wcel wi cdif c0 cv cin wex eleq1 biimpd
      ordtr trss eldifi syl5 difin0ss comm syl6 syl ad2antll imp32 wel weq w3o
      cep wwe wecmpep ordwe ssel imp im2an syl2an adantalr biimpcd eldifn
      nsyli adantall adantl trel exp3a com23 exp adantld ecased exp44 com34
      imp31 ssrdv adantarr jca eqss sylibr ad2antrl syl13 19.23adv difss tz7.5
      mpan21 exp4b adantrd pm2.43i pssdifn0 syl7 exp4a pm2.43d imp3a wfr tz7.2
      ordfr sylan adantr impbid ) AEZBFZGZBAHZBAIJZGZBAKZXCXDXEXGXCXDXEXGLXCXDX
      DXEXGXCXDABMZNIJZXGXFXCXDXIXGLZLZXCXAXKXBXCXDXAXJXCXDXAXIXGXCXDGZCOZXHKZX
      HXMPNIZGZCQZXGXAXIGXLXPXGCXLXPXGXMBIZXMAKZXGXLXPGZXRXSXGXMBARSXTXMBHZBXMH
      ZGXRXTYAYBXLXNXOYAXAXNXOYALZLZXBXDXAAFZYDATZYEXNXMAHZYCYEXSYGXNAXMUAXMABU
      BZUCXOYGYAABXMUDUEUFUGUHUIXLXNYBXOXLXNGDBXMXCXDXNDOZBKZDCUJZLXCXDYJXNYKXC
      XDYJXNYKXCXDYJGZXNGZGYKDCUKZCDUJZXAYMYKYNYOULZXBAUMUNYIAKZXSGYPXAYMDCAUOA
      UPYLYQXNXSXDYJYQBAYIUQURYHUSUTVAYMYNJZXCYJXNYRXDYJXNYRYJYNXMBKZXNYNYJYSYI
      XMBRVBXMABVCZVDURVEVFXBYMYOJZXAXBYLXNUVAXBYJXNUVALZXDXBYJUVBXBYJGYOYSXNXB
      YJYOYSLXBYOYJYSXBYOYJYSBXMYIVGVHVIURYTVDVJVKUIVEVLVMVNVOVPVQVRXMBVSVTXNXS
      XLXOYHWAWBVJWCXAXHAHXIXQABWDCAXHWEWFUCWGVIWHWIBAWJWKWLWMWNXAXGXFLXBXAXGXF
      YEAUMWOZGXGXFXAABWPXAYEUVCYFAWQVRWRVJWSWT $.
      $([5-May-94]$)
  $}

  $( Corollary 7.8 of Takeuti and Zaring, p. 37. $)
  tz7.8 $p |- ( ( Ord A /\ Ord B ) ->
              ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
    ( word wcel wss wceq wn wa wb wtr tz7.7 ordtr sylan5 bicomd ancoms ) BCZACZ
    ABDZABEABFGHZIPQHSRPAJSRIQBAKALMNO $.
    $([25-Nov-95]$)

  $( Corollary 7.8 of Takeuti and Zaring, p. 37. $)
  ordelpss $p |- ( ( Ord A /\ Ord B ) -> ( A e. B <-> A (. B ) ) $=
    ( word wa wcel wss wceq wn wpss tz7.8 dfpss2 syl6bbr ) ACBCDABEABFABGHDABIA
    BJABKL $.
    $([17-Jun-98]$)

  $( Subclass is equivalent to membership or equality for ordinals. $)
  ordssel $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wa wss wcel wceq wo wn wi tz7.8 biimprd exp3a orcom df-or bitr
    syl6ibr pm3.26 syl6bi eqimss jctir jaob sylibr impbid ) ACBCDZABEZABFZABGZH
    ZUEUFUHIZUGJZUIUEUFUJUGUEUGUFUJDZABKZLMUIUHUGHUKUGUHNUHUGOPQUEUGUFJZUHUFJZD
    UIUFJUEUNUOUEUGULUFUMUFUJRSABTUAUGUFUHUBUCUD $.
    $([25-Nov-95]$)

  $( Proposition 7.9 of Takeuti and Zaring, p. 37. $)
  tz7.9 $p |- ( ( Ord A /\ Ord B ) -> Ord ( A i^i B ) ) $=
    ( word cin wtr wa wss inss2 trssord mp3an2 trin ordtr syl2an sylan anabss3
    ) ACZBCZABDZCZREZQSPQFTRBGQSABHRBIJAEBETPQABKALBLMNO $.
    $([9-May-94]$)

  ${
    $d x y A $.  $d x y B $.
    $( The intersection of two ordinal numbers is an ordinal number. $)
    onin $p |- ( ( A e. On /\ B e. On ) -> ( A i^i B ) e. On ) $=
      ( con wcel wa cin word tz7.9 eloni syl2an cvv wb pm3.26 zfausclg elong
      3syl mpbird ) ACDZBCDZEZABFZCDZUAGZAGBGUCRSABHAIBIJTRUAKDUBUCLRSMABCNUAKO
      PQ $.
      $([7-Apr-95]$)
  $}

  $( Proposition 7.10 of Takeuti and Zaring, p. 38. $)
  tz7.10 $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ A = B \/ B e. A ) ) $=
    ( word wa wcel wceq wo w3o wss cin wn tz7.9 ordeirr syl elin incom eleq1i
    bilan bitr bineg ianor sylib inss1 ordssel mpbii sylan anabss1 ord sseqin1
    cleqcom syl6ibr anabss4 im2ord mpd wb ancoms bi2ord mpbid df-3or orabs
    bilor or23 or4 3bitr4 sylibr ) ACZBCZDZABEZABFZGZBAEZBAFZGZGZVIVJVLHZVHABIZ
    BAIZGZVOVHABJZAEZKZBAJZBEZKZGZVSVHVTVTEZKZWFVHVTCZWHABLZVTMNWHWAWDDZKWFWGWK
    WGWAVTBEZDWKVTABOWLWDWAVTWCBABPQRSTWAWDUASUBVHWBVQWEVRVHWBVTAFZVQVHWAWMVFVG
    WAWMGZWIVFWNVHWIVFDVTAIWNABUCVTAUDUEWJUFUGUHVQAVTFWMABUIAVTUJSUKVHWEWCBFZVR
    VHWDWOVFVGWDWOGZWCCZVGWPVGVFDWQVGDWCBIWPBAUCWCBUDUEBALUFULUHVRBWCFWOBAUIBWC
    UJSUKUMUNVHVQVKVRVNABUDVGVFVRVNUOBAUDUPUQURVPVKVLGZVOVIVJVLUSVIVLGZVJGWSVJV
    MGZGWRVOVJWTWSVJVJVJGWTVJUTVJVMVJABUJVASVAVIVJVLVBVIVJVLVMVCVDSVE $.
    $([10-May-94]$)

  $( Trichotomy law for ordinals. $)
  ordssnel $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> -. B e. A ) ) $=
    ( word wa wss wcel wceq wo wn ordssel wi ordn2lp imnan sylibr eleq2 binegd
    ordeirr syl5bir comm jaao w3o tz7.10 df-3or orcom df-or 3bitr sylib impbid
    bitrd ) ACZBCZDZABEABFZABGZHZBAFZIZABJULUOUQUJUMUQUKUNUJUMUPDIUMUQKABLUMUPM
    NUNUKUQUNUQBBFZIUKUNUPURABBOPBQRSTULUMUNUPUAZUQUOKZABUBUSUOUPHUPUOHUTUMUNUP
    UCUOUPUDUPUOUEUFUGUHUI $.
    $([25-Mar-95]$)

  $( Trichotomy law for ordinals. $)
  ordtri $p |- ( ( Ord A /\ Ord B ) ->
               ( A e. B <-> -. ( A = B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn wb wss ordssel cleqcom bilor orcom bitr syl6bb
    ordssnel bitr3d ancoms bicon2d ) ACZBCZDABEZBAFZGZABFZUBUAUEUFHZIUBUADZBAJZ
    UEUGUHUIUDBAEZGZUEBAKUKUDUCGUEUJUCUDBALMUDUCNOPBAQRST $.
    $([25-Nov-95]$)

  $( Trichotomy law for ordinals. $)
  ordtri2 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> -. ( A e. B \/ B e. A ) ) ) $=
    ( word wa wceq wcel wo wn eleq2 binegd ordeirr syl5bi syl5bir im2and ioran
    syl6ibr comm w3o wi tz7.10 df-3or or23 df-or 3bitr sylib impbid ) ACZBCZDZA
    BEZABFZBAFZGZHZUJUIUNUJUIUKHZULHZDUNUJUGUOUHUPUJAAFZHUOUGUJUQUKABAIJAKLUJUP
    BBFZHUHUJULURABBIJBKMNUKULOPQUIUKUJULRZUNUJSZABTUSUKUJGULGUMUJGUTUKUJULUAUK
    UJULUBUMUJUCUDUEUF $.
    $([18-Oct-95]$)

  $( An ordinal and its singleton are disjoint. $)
  orddisj $p |- ( Ord A -> ( A i^i { A } ) = (/) ) $=
    ( word wcel wn csn cin c0 wceq ordeirr disjsn sylibr ) ABAACDAAEFGHAIAAJK
    $.
    $([19-May-98]$)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( The ordinal class is founded.  This lemma is needed for
       ~ tz7.12 in order to eliminate the need for the Axiom of Regularity. $)
    onfr $p |- E Fr On $=
      ( vx vz vy con cep wfr cv wss c0 wceq wn wa wel cin wex wi dfepfr weq
      a13b ineq2 cleq1d bi2and biimprd a4w exp comm a1d word wcel ssel visset
      elon syl6ib wtr inss2 zfauscl epfrc mpani inss1 ssi trss syl5 sseqin2
      inass syl6eqr sylbi biimprcd sylan9 imp im2an exp32 pm2.43b com23 imp3a
      19.22dv ordfr ordtr sylan13 syl6 com3r pm2.61i 19.23adv nnull syl5ib
      mpgbir ) DEFAGZDHZWFIJKZLBAMZWFBGZNZIJZLZBOZPAABDQWGWHWNWGCAMZCOWNWHWGWOW
      NCWFCGZNZIJZWGWOWNPZPWRWSWGWOWRWNWOWRWNWOWRLZWMBCBCRZWMWTXAWIWOWLWRBCASXA
      WKWQIWJWPWFTUAUBUCUDUEUFUGWGWOWRKZWNWGWOWPUHZXBWNPZWGWOWPDUIXCWFDWPUJWPCU
      KULUMWPEFZWPUNZXDXCXEXBWJWQUIZWQWJNZIJZLZBOZXFWNXEWQWPHZXBXKWFWPUOZXEXLXB
      LXKBWPWQWFWPAUKUPUQUEURXFXJWMBXFXGXIWMXFXIXGWMXFXIXGWMPXFXILZXGWMXGXNXGWM
      XGWIXNXGLWLWQWFWJWFWPUSUTXNXGWLXFXGWJWPHZXIWLXFBCMXOXGWPWJVAWQWPWJXMUTVBX
      OWLXIXOWKXHIXOWJWPWJNZJZWKXHJWJWPVCXQWKWFXPNXHWJXPWFTWFWPWJVDVEVFUAVGVHVI
      VJVKVLUEVMVNVOVHWPVPWPVQVRVSVTWAWBCWFWCWDVIWE $.
      $([17-May-94]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The ordinal class is an ordinal.  Proposition 7.12 of Takeuti and
       Zaring, p. 38, but without using the Axiom of Regularity. $)
    tz7.12 $p |- Ord On $=
      ( vx vy con word wtr cep wwe wa cv wcel wss wi dftr3 wel tz7.6 visset
      elon sylanb exp syl6ibr ssrdv mpgbir wfr wbr weq w3o wral onfr tz7.10
      epel pm4.2 bi3or sylibr eloni syl2an rgen2 pm3.2i dfwe2 mpbir df-ord ) CD
      CEZCFGZHVAVBVAAIZCJZVCCKLAACMVDBVCCVDBANZBIZDZVFCJZVDVEVGVCDZVEVGVDVCVFOV
      CAPQRSVFBPQTUAUBVBCFUCZVCVFFUDZABUEZVFVCFUDZUFZBCUGACUGZHVJVOUHVNABCVIVGV
      NVDVHVIVGHABNZVLVEUFVNVCVFUIVKVPVLVLVMVEABUJVLUKBAUJULUMVCUNVFUNUOUPUQABC
      FURUSUQCUTUS $.
      $([17-May-94]$)
  $}

  $( ` x ` is dummy. $)
  $( No set contains all ordinal numbers.  Proposition 7.13 of Takeuti and
       Zaring, p. 38, but without using the Axiom of Regularity.  Also known
       as the Burali-Forti paradox (Enderton, p. 194).  It is called a
       "paradox" because if we assume there is such a set (which was the
       assumption before Russell - it was assumed that any definable
       mathematical object must necessarily exist), then we arrive at a
       contradiction.  Now we know better.  Note that Burali-Forti was one
       person, not two. $)
  tz7.13 $p |- -. On e. V $=
    ( con cvv wcel word wn tz7.12 ordeirr ax-mp elong mpbiri mto ) ABCZAACZADZM
    EFAGHLMNFABIJK $.
    $([18-May-94]$)

  $( Corollary 7.14 of Takeuti and Zaring, p. 38. $)
  tz7.14 $p |- ( Ord A -> ( A e. On \/ A = On ) ) $=
    ( word con wcel wceq wo cvv tz7.13 elisset mto w3o tz7.12 tz7.10 mpan2
    df-3or sylib ord mt3i ) ABZACDZACEZFZCADZUCCGDHCAIJSUBUCSTUAUCKZUBUCFSCBUDL
    ACMNTUAUCOPQR $.
    $([18-May-94]$)

  $( Corollary 7.15 of Takeuti and Zaring, p. 38. $)
  tz7.15 $p |- ( Ord A -> A (_ On ) $=
    ( word con wcel wss wceq wn wa wb tz7.12 tz7.8 mpan2 pm3.26 syl6bi tz7.14
    ord eqimss syl6 pm2.61d ) ABZACDZACEZTUAUBACFZGZHZUBTCBUAUEIJACKLUBUDMNTUAG
    UCUBTUAUCAOPACQRS $.
    $([18-May-94]$)

  $( An ordinal number is a subset of ` On `. $)
  onsst $p |- ( A e. On -> A (_ On ) $=
    ( con wcel word wss eloni tz7.15 syl ) ABCADABEAFAGH $.
    $([5-Jun-94]$)

  ${
    $d x A $.
    $( The Principle of Transfinite Induction.  Theorem 7.17 of Takeuti and
       Zaring, p. 39. $)
    tfi   $p |- ( ( A (_ On /\ A. x ( x e. On -> ( x (_ A -> x e. A ) ) ) ->
                A = On ) $=
      ( con wss cv wcel wi wal wa wceq cdif cin c0 wex wn onsst difin0ss comm
      syl syl4d a2i eldifi syl5 imp3a eldifn adantr pm2.65 mpi 19.20i 19.7
      sylib ssdif0 bineg difss word tz7.12 tz7.5 mpan sylbi nsyl2 imlan eqss
      sylibr ) BCDZAEZCFZVEBDZVEBFZGZGZAHZIVDCBDZIBCJVKVLVDVKVECBKZFZVMVELMJZIZ
      ANZVLVKVPOZAHVQOVJVRAVJVPVHGZVRVJVNVOVHVJVFVOVHGZVNVFVIVTVFVOVGVHVFVECDZV
      OVGGVEPVOWAVGCBVEQRSTUAVECBUBUCUDVSVPVHOZGVRVNWBVOVECBUEUFVPVHUGUHSUIVPAU
      JUKVLOVMMJZOZVQVLWCCBULUMVMCDZWDVQCBUNCUOWEWDIVQUPACVMUQURURUSUTVABCVBVC
      $.
      $([10-Aug-94]$)
  $}

  ${
    $d x A $.
    tz7.17.1 $e |- A (_ On $.
    tz7.17.2 $e |- ( x e. On -> ( x (_ A -> x e. A ) ) $.
    $( The Principle of Transfinite Induction (inference rule).  Theorem 7.17
       of Takeuti and Zaring, p. 39. $)
    tz7.17   $p |- A = On $=
      ( con wss cv cdif wcel cin c0 wceq wa wex wi eldifi onsst difin0ss comm
      syl syld imp wn eldifn adantr pm2.65i nex ssdif0 bineg difss word tz7.12
      tz7.5 mpan sylbi mt3 eqssi ) BECEBFZAGZEBHZIZUTUSJKLZMZANZVCAVCUSBIZVAVBV
      EVAUSEIZVBVEOUSEBPVFVBUSBFZVEVFUSEFZVBVGOUSQVBVHVGEBUSRSTDUATUBVAVEUCVBUS
      EBUDUEUFUGURUCUTKLZUCZVDURVIEBUHUIUTEFZVJVDEBUJEUKVKVJMVDULAEUTUMUNUNUOUP
      UQ $.
      $([31-Jul-94]$)
  $}

  ${
    $d y ph z $.  $d x y z $.  $( ` z ` is dummy. $)
    tfis.1 $e |- ( x e. On -> ( A. y ( y e. x -> ( ph x | y ) ) -> ph ) ) $.
    $( Transfinite Induction Schema.  Compare Exercise 25 of Enderton,
       p. 200. $)
    tfis   $p |- ( x e. On -> ph ) $=
      ( vz cv con wcel wa cab ssab wss wsb wel wi wal ax-17 hbsbv hbim hbal
      visset weq eleq1 a14b birimd bialdv sbequ12 bi2imd vtoclb df-ss hban
      bi2and abclf pm3.27bd syl3 19.20i sylbi syl5 anc2li syl6ibr tz7.17
      cleqcomi cleqabi ) BFZGHZVEAVEAIZBGVFBJZGEVGABGKEFZGHZVHVGLZVIABEMZIZVHVG
      HVIVJVKVICENZABCMZOZCPZVKVJVECBNZVNOZCPZAOZOVIVPVKOZOBVHVIWABVIBQZVPVKBVO
      BCVMVNBVMBQABCRZSTABERZSSEUAZBEUBZVEVIVTWAVDVHGUCZWFVSVPAVKWFVRVOCWFVQVMV
      NBECUDUEUFABEUGZUHUHDUIVJVMCFZVGHZOZCPVPCVHVGUJWKVOCWJVNVMWJWIGHZVNVFWLVN
      IBWIWLVNBWLBQWCUKCUABCUBVEWLAVNVDWIGUCABCUGULUMUNUOUPUQURUSVFVLBVHVIVKBWB
      WDUKWEWFVEVIAVKWGWHULUMUTVAVBVCUN $.
      $([1-Aug-94]$)
  $}

  ${
    $d y ph $.  $d x y $.
    tfis2f.1 $e |- ( ps -> A. x ps ) $.
    tfis2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2f.3 $e |- ( x e. On -> ( A. y ( y e. x -> ps ) -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2f   $p |- ( x e. On -> ph ) $=
      ( cv con wcel wel wi wal wsb ddel4 bilim bial syl5ib tfis ) ACDCHIJDCKZBL
      ZDMATACDNZLZDMGUCUADUBBTABCDEFOPQRS $.
      $([18-Aug-94]$)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x y $.
    tfis2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2.2 $e |- ( x e. On -> ( A. y ( y e. x -> ps ) -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2   $p |- ( x e. On -> ph ) $=
      ( ax-17 tfis2f ) ABCDBCGEFH $.
      $([18-Aug-94]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The union of a class of ordinals in an ordinal.  Proposition 7.19 of
       Takeuti and Zaring, p. 40. $)
    tz7.19 $p |- ( A (_ On -> Ord U. A ) $=
      ( vx vy cuni wtr con wss word tz7.12 trssord 3exp mpii cv wcel wi wal
      wel wa wex ssel eloni ordtr trss 3syl syl6 com23 ancr imp3a ssuni
      19.23adv eluni syl5ib 19.21aiv dftr3 sylibr tz7.6 exp visset elon
      syl6ibr syl ssrdv syl13 ) ADZEZVDFGZVDHZAFGZVEVFFHZVGIVEVFVIVGVDFJKLVHBMZ
      VDNZVJVDGZOZBPVEVHVMBVHBCQZCMZANZRZCSZVLVKVHVQVLCVHVQVJVOGZVPRZVLVHVNVPVT
      VHVNVPVSOVPVTOVHVPVNVSVHVPVOFNZVNVSOZAFVOTZWAVOHZVOEWBVOUAZVOUBVOVJUCUDUE
      UFVPVSUGUEUHVJVOAUIUEUJCVJAUKZULUMBVDUNUOVHBVDFVHVRVJFNZVKVHVQWGCVHVNVPWG
      VHVPVNWGVHVPWAVNWGOZWCWAWDWHWEWDVNVJHZWGWDVNWIVOVJUPUQVJBURUSUTVAUEUFUHUJ
      WFULVBVC $.
      $([30-May-94]$)
  $}

  $( Transitive law for ordinals. $)
  ontrt $p |- ( ( A e. B /\ B e. On ) -> A e. On ) $=
    ( con wcel wa word tz7.6 eloni sylan wb elong adantl mpbird ancoms ) BCDZAB
    DZACDZOPEQAFZBFPROBAGBHIPQRJOABKLMN $.
    $([12-Aug-94]$)

  $( Transitive law for ordinals.  Theorem 7M(b) of Enderton, p. 192. $)
  ontr1t $p |- ( A e. On -> ( ( B e. C /\ C e. A ) -> B e. A ) ) $=
    ( con wcel word wtr wa wi eloni ordtr trel 3syl ) ADEAFAGBCECAEHBAEIAJAKABC
    LM $.
    $([11-Aug-94]$)

  $( Relationship between membership and proper subset of an ordinal number. $)
  onelpsst $p |- ( ( A e. On /\ B e. On ) ->
               ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
    ( word wcel wss wceq wn wa wb con tz7.8 eloni syl2an ) ACBCABDABEABFGHIAJDB
    JDABKALBLM $.
    $([15-Sep-95]$)

  $( Relationship between subset and membership of an ordinal number. $)
  onsselt $p |- ( ( A e. On /\ B e. On ) ->
                 ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
    ( word wss wcel wceq wo wb con ordssel eloni syl2an ) ACBCABDABEABFGHAIEBIE
    ABJAKBKL $.
    $([15-Sep-95]$)

  $( An element of an ordinal number is a subset of the number. $)
  onelsst $p |- ( A e. On -> ( B e. A -> B (_ A ) ) $=
    ( con wcel word wtr wss wi eloni ordtr trss 3syl ) ACDAEAFBADBAGHAIAJABKL
    $.
    $([5-Jun-94]$)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( The intersection (infimum) of a non-empty ordinal class belongs to the
       class.  Compare Exercise 4 of Takeuti and Zaring, p. 45. $)
    ordint $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. A ) $=
      ( vx vy vz con wss c0 wceq wn cint wcel cv cin wa wex wi wel wal ssel
      word wb ordssnel eloni syl2an syl6bir exp sylan9 com4r imp a2d 19.20dv
      disj visset elint 3imtr4g imdistani sylan5 exp32 com4l imp32 ssrdv intss
      ad2antrl jca eqss sylibr eleq1d biimpd com34 pm2.43d imp3a 19.23adv
      tz7.12 tz7.5 mpan syl5 anabsi5 ) AEFZAGHIZAJZAKZVRBLZAKZAWBMGHZNZBOZWAVRV
      SNZVRWEWABVRWCWDWAVRWCWDWAPVRWCWDWCWAVRWCWDWCWAPVRWENZWCWAWHWBVTAWHWBVTFZ
      VTWBFZNWBVTHWHWIWJWHCWBVTVRWCWDCBQZCLZVTKZPWKVRWCWDWMWKVRWCWDWMPZWKVRWBEK
      ZNZWNVRWCNWKWPNZDLZAKZDBQIZPZDRWSCDQZPZDRWDWMWQXAXCDWQWSWTXBWKWPWSWTXBPPW
      PWSWTWKXBVRWSWREKZWOWTWKXBPZPZAEWRSWOXDXFWOXDNWTWBWRFZXEWBTWRTXGWTUAWOXDW
      BWRUBWBUCWRUCUDWBWRWLSUEUFUGUHUIUJUKDAWBULDWLACUMUNUOVRWCWOAEWBSUPUQURUSU
      TVAWCWJVRWDWBAVBVCVDWBVTVEVFVGVHURVIVJVKVLETWGWFVMBEAVNVOVPVQ $.
      $([31-Jan-97]$)
  $}

  $( The intersection of a non-empty collection of ordinals is an ordinal.
     Compare Exercise 6 of Takeuti and Zaring, p. 44. $)
  onint $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. On ) $=
    ( con wss c0 wceq wn wa cint wcel ordint wi ssel adantr mpd ) ABCZADEFZGAHZ
    AIZQBIZAJORSKPABQLMN $.
    $([29-Jan-97]$)

  $( The intersection of a collection of ordinals is its minimum. $)
  onmin $p |- ( ( A (_ On /\ B e. A ) -> -. B e. |^| A ) $=
    ( con wss wcel wa cint wn intss adantl word wb ordssnel c0 wceq onint
    noel2 sylan5 eloni syl ssel imp sylan13 mpbid ) ACDZBAEZFZAGZBDZBUHEHZUFUIU
    EBAIJUHKZBKZUIUJLUGUHBMUGUHCEZUKUEANOHUMUFAPABQRUHSTUGBCEZULUEUFUNACBUAUBBS
    TUCUD $.
    $([2-Feb-97]$)

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    onminex.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( If a wff is true for an ordinal, it is true for a minimum ordinal. $)
    onminex $p |- ( E. x e. On ph -> E. x e. On ( ph /\ A. y e. x -. ps ) ) $=
      ( cv con wcel wa wex wn wral wrex cab c0 wceq wel wi wal cint hbab1
      hbint hbel ax-17 hbim hbal hban eleq1 eleq2 birimd bialdv bi2and cla4egf
      anabsi5 wss ssab ordint mpan ontrt onint sylan5 exp visset weq abcl
      onmin sylbir pm2.03d syl6 pm2.43b 19.21aiv sylan13 noel3 abid df-ral
      bi2an anass bitr3 biex 3imtr3 df-rex 3imtr4 ) CFZGHZAIZCJZWDABKZDWCLZIZIZ
      CJZACGMWICGMWECNZOPKZWCWLHZDCQZWGRZDSZIZCJZWFWKWLTZWLHZDFZWTHZWGRZDSZWSWM
      XAXEWSWRXAXEICDWTWLCDWLWECDUAZUBZXAXECCDDWTWLXGXFUCXDCDXCWGCXGWGCUDUEUFUG
      WCWTPZWNXAWQXEWCWTWLUHXHWPXDDXHWOXCWGWCWTXBUIUJUKULUMUNWLGUOZWMXAACGUPZWL
      UQURWMXDDWMXCWGXCWMXBGHZXDXCWMXKXCWTGHZXKWMXBWTUSXIWMXLXJWLUTURVAVBXKBXCX
      KBXCKZXKBIZXBWLHZXMWEXNCXBDVCCDVDWDXKABWCXBGUHEULVEXIXOXMXJWLXBVFURVGVBVH
      VIVJVKVLWECVMWRWJCWRWEWHIWJWEWNWHWQWECVNWGDWCVOVPWDAWHVQVRVSVTACGWAWICGWA
      WB $.
      $([2-Feb-97]$)
  $}

  $( The empty set is an ordinal. $)
  ord0 $p |- Ord (/) $=
    ( c0 word wtr cep wwe wa tr0 we0 pm3.2i df-ord mpbir ) ABACZADEZFLMGDHIAJK
    $.
    $([11-May-94]$)

  $( The empty set is an ordinal.  Corollary 7N(b) of Enderton, p. 193. $)
  0elon $p |- (/) e. On $=
    ( c0 con wcel word ord0 zfnull elon mpbir ) ABCADEAFGH $.
    $([17-Sep-93]$)

  $( A non-empty ordinal contains the empty set. $)
  ord0eln0 $p |- ( Ord A -> ( (/) e. A <-> -. A = (/) ) ) $=
    ( word c0 wcel wceq wn wi noel2 a1i wo ord0 wa noel ordtri bicon2d mpbiri
    mpan2 ord impbid ) ABZCADZACEZFZUAUCGTACHITUBUATCBZUBUAJZKTUDLZUEACDZFAMUFU
    GUEACNOPQRS $.
    $([25-Nov-95]$)

  $( The empty set is not a limit ordinal. $)
  nlim0 $p |- -. Lim (/) $=
    ( c0 wlim wceq cleqid word wn cuni w3a df-lim 3simp2 sylbi mt2 ) ABZAACZADM
    AEZNFZAAGCZHPAIOPQJKL $.
    $([24-Mar-95]$)

  $( A limit ordinal is ordinal. $)
  limord $p |- ( Lim A -> Ord A ) $=
    ( wlim word c0 wceq wn cuni w3a df-lim 3simp1 sylbi ) ABACZADEFZAAGEZHLAILM
    NJK $.
    $([4-May-95]$)

  $( A limit ordinal is its own supremum (union). $)
  limuni $p |- ( Lim A -> A = U. A ) $=
    ( wlim word c0 wceq wn cuni w3a df-lim 3simp3 sylbi ) ABACZADEFZAAGEZHNAILM
    NJK $.
    $([4-May-95]$)

  $( A limit ordinal contains the empty set. $)
  0ellim $p |- ( Lim A -> (/) e. A ) $=
    ( wlim c0 wcel wceq wn nlim0 limeq mtbiri con2 word wb limord ord0eln0 syl
    mpbird ) ABZCADZACEZFZSQSQCBGACHIJQAKRTLAMANOP $.
    $([15-May-94]$)

  $( A limit ordinal set is an ordinal number. $)
  limelon $p |- ( A e. B -> ( Lim A -> A e. On ) ) $=
    ( wcel con word wlim elong limord syl5bir ) ABCADCAEAFABGAHI $.
    $([4-May-95]$)

  $( The class of all ordinals in not empty. $)
  onne0 $p |- -. On = (/) $=
    ( c0 con wcel wceq wn 0elon noel2 ax-mp ) ABCBADEFBAGH $.
    $([17-Sep-95]$)

  $( Equality of successors. $)
  suceq $p |- ( A = B -> suc A = suc B ) $=
    ( wceq csn cun csuc sneq uneq2d uneq1 eqtrd df-suc 3eqtr4g ) ABCZAADZEZBBDZ
    EZAFBFMOAPEQMNPAABGHABPIJAKBKL $.
    $([30-Aug-93]$)

  $( Membership in a successor. $)
  elsuci $p |- ( A e. suc B -> ( A e. B \/ A = B ) ) $=
    ( csuc wcel csn wo wceq cun df-suc eleq2i elun bitr elsni imlor sylbi ) ABC
    ZDZABDZABEZDZFZRABGZFQABSHZDUAPUCABIJABSKLTUBRABMNO $.
    $([6-Jun-94]$)

  $( Membership in a successor. $)
  elsucg $p |- ( A e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
    ( wcel csn wo wceq csuc elsncg bilord cun df-suc eleq2i elun bitr syl5bb )
    ACDZABDZABEZDZFZRABGZFABHZDZQTUBRABCIJUDABSKZDUAUCUEABLMABSNOP $.
    $([15-Sep-95]$)

  $( Membership in a successor. $)
  elelsuc $p |- ( A e. B -> A e. suc B ) $=
    ( wcel csuc wceq wo orc elsucg mpbird ) ABCZABDCJABEZFJKGABBHI $.
    $([20-Jun-98]$)

  $( A proper class is its own successor. $)
  sucprc $p |- ( -. A e. V -> A = suc A ) $=
    ( cvv wcel wn c0 cun csn csuc wceq uneq2 snprc cleqcom 3imtr4 un0 df-suc
    3eqtr4g ) ABCDZAEFZAAGZFZAAHSEITRIQRTISEAJAKRTLMANAOP $.
    $([3-Apr-95]$)

  ${
    unisuc.1 $e |- A e. V $.
    $( A transitive set is equal to the union of its successor.  Generalization
       of Theorem 4E, p. 72, of Enderton. $)
    unisuc $p |- ( Tr A <-> A = U. suc A ) $=
      ( cuni wss cun wceq wtr csuc ssequn1 df-tr csn undist df-suc unieqi
      unisn uneq2i 3eqtr4 cleq2i 3bitr4 ) ACZADATAEZFAGAAHZCZFTAIAJUCUAAAAKZEZC
      TUDCZEUCUAAUDLUBUEAMNAUFTABOPQRS $.
      $([30-Aug-93]$)
  $}

  $( Part of Proposition 7.23 of Takeuti and Zaring, p. 41 (generalized). $)
  tz7.23b $p |- A (_ suc A $=
    ( csn cun csuc ssun1 df-suc sseqtr4 ) AAABZCADAHEAFG $.
    $([31-May-94]$)

  $( The successor exists iff the class exists. $)
  sucexbg $p |- ( A e. V <-> suc A e. V ) $=
    ( cvv wcel csn wa cun csuc unexbg snex biantru df-suc eleq1i 3bitr4 ) ABCZA
    DZBCZEAOFZBCNAGZBCAOHPNAIJRQBAKLM $.
    $([22-Jun-98]$)

  $( The successor of a set is a set (generalization). $)
  sucexg $p |- ( A e. B -> suc A e. V ) $=
    ( wcel cvv csuc elisset sucexbg sylib ) ABCADCAEDCABFAGH $.
    $([5-Jun-94]$)

  ${
    sucex.1 $e |- A e. V $.
    $( The successor of a set is a set. $)
    sucex   $p |- suc A e. V $=
      ( cvv wcel csuc sucexg ax-mp ) ACDAECDBACFG $.
      $([30-Aug-93]$)
  $}

  ${
    sucid.1 $e |- A e. V $.
    $( A set belongs to its successor. $)
    sucid   $p |- A e. suc A $=
      ( csn wcel csuc snid wo cun df-suc eleq2i elun bitr biimpr olci ax-mp )
      AACZDZAAEZDZABFAADZQSSTQGZSAAPHZDUARUBAAIJAAPKLMNO $.
      $([22-Jun-94]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Part of Proposition 7.23 of Takeuti and Zaring, p. 41 (generalized). $)
    sucidg $p |- ( A e. B -> A e. suc A ) $=
      ( vx cv csuc wcel wceq id suceq eleq12d visset sucid vtoclg ) CDZNEZFAAEZ
      FCABNAGZNAOPQHNAIJNCKLM $.
      $([25-Mar-95]$)
  $}

  $( No successor is empty. $)
  nsuceq0 $p |- -. suc A = (/) $=
    ( cvv wcel csuc c0 wceq wn noel eleq2 sucidg syl5bi comm mtoi sucprc
    cleq1d zfnull eleq1 mpbiri syl6bir pm2.16d pm2.43i pm2.61i ) ABCZADZEFZGZUC
    UEAECZAHUEUCUGUEAUDCUGUCUDEAIABJKLMUCGZUFUHUEUCUHUEAEFZUCUHAUDEANOUIUCEBCPA
    EBQRSTUAUB $.
    $([3-Apr-95]$)

  ${
    eqelsuc.1 $e |- A e. V $.
    $( A set belongs to the successor of an equal set. $)
    eqelsuc   $p |- ( A = B -> A e. suc B ) $=
      ( wceq csuc wcel sucid suceq eleq2d mpbii ) ABDZAAEZFABEZFACGKLMAABHIJ $.
      $([18-Aug-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    trsuc.1 $e |- B e. V $.
    $( A set whose sucessor belongs to a transitive class also belongs. $)
    trsuc   $p |- ( ( Tr A /\ suc B e. A ) -> B e. A ) $=
      ( vx wtr csuc wcel cv wceq wi wal trel exp3a csn elsn wo cun df-suc
      eleq2i elun bitr biimpr olci sylbir syl5 com23 19.21adv clel2 syl6ibr
      imp ) AEZBFZAGZBAGZUKUMDHZBIZUOAGZJZDKUNUKUMURDUKUPUMUQUKUOULGZUMUQJUPUKU
      SUMUQAUOULLMUPUOBNZGZUSDBOUOBGZVAUSUSVBVAPZUSUOBUTQZGVCULVDUOBRSUOBUTTUAU
      BUCUDUEUFUGDBACUHUIUJ $.
      $([21-Jun-94]$)
  $}

  $( A  subset of an ordinal is a member of its successor. $)
  onsssuct $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B <-> A e. suc B ) ) $=
    ( con wcel wa wss wceq wo csuc onsselt wb elsucg adantr bitr4d ) ACDZBCDZEA
    BFABDABGHZABIDZABJORQKPABCLMN $.
    $([15-Sep-95]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Proposition 7.24 of Takeuti and Zaring, p. 41. $)
    tz7.24 $p |- ( A e. On -> suc A e. On ) $=
      ( vx con wcel csuc word wtr wss tz7.12 trssord 3exp mpii cv wi wal csn
      wo onelsst wceq elsn eqimss sylbi a1i im2ord cun df-suc eleq2i elun bitr
      orabs 3imtr4g tz7.23b sstr mpi syl6 19.21aiv dftr3 sylibr wa onsst snssi
      jca unss sseq1i syl13 cvv wb sucexg elong syl mpbird ) ACDZAEZCDZVMFZVMGZ
      VMCHZVOVLVPVQCFZVOIVPVQVRVOVMCJKLVLBMZVMDZVSVMHZNZBOVPVLWBBVLVTVSAHZWAVLV
      SADZVSAPZDZQZWCWCQVTWCVLWDWCWFWCAVSRWFWCNVLWFVSASWCBATVSAUAUBUCUDVTVSAWEU
      EZDWGVMWHVSAUFZUGVSAWEUHUIWCUJUKWCAVMHWAAULVSAVMUMUNUOUPBVMUQURVLWHCHZVQV
      LACHZWECHZUSWJVLWKWLAUTACVAVBAWECVCURVMWHCWIVDURVEVLVMVFDVNVOVGACVHVMVFVI
      VJVK $.
      $([6-Jun-94]$)
  $}

  $( Proposition 7.25 of Takeuti and Zaring, p. 41. $)
  tz7.25 $p |- ( A e. On -> -. ( A e. B /\ B e. suc A ) ) $=
    ( con wcel word csuc wa wn eloni wo ordn2lp ordeirr jca ioran sylibr wceq
    elsuci imlan andi sylib eleq2 biimpac imlor syl nsyl ) ACDAEZABDZBAFDZGZHAI
    UFUGBADZGZAADZJZUIUFUKHZULHZGUMHUFUNUOABKALMUKULNOUIUKUGBAPZGZJZUMUIUGUJUPJ
    ZGURUHUSUGBAQRUGUJUPSTUQULUKUPUGULBAAUAUBUCUDUEUD $.
    $([9-Jun-94]$)

  $( Generalized Proposition 7.25 of Takeuti and Zaring, p. 41. $)
  tz7.25g $p |- ( Ord A -> -. ( A e. B /\ B e. suc A ) ) $=
    ( word con wcel wceq wo csuc wa wn tz7.14 tz7.25 cvv tz7.13 elisset mto
    eleq1 mtbiri orc syl ianor sylibr jaoi ) ACADEZADFZGABEZBAHEZIJZAKUDUHUEABL
    UEUFJZUGJZGZUHUEUIUKUEUFDBEZULDMENDBOPADBQRUIUJSTUFUGUAUBUCT $.
    $([21-Jun-98]$)

  $( The successor of an ordinal is ordinal. $)
  ordsuc $p |- ( Ord A <-> Ord suc A ) $=
    ( cvv wcel word csuc wb con elong tz7.24 eloni syl syl6bir tz7.6 exp
    sucidg syl5 comm impbid wn wceq sucprc ordeq pm2.61i ) ABCZADZAEZDZFZUDUEUG
    UDUEAGCZUGABHUIUFGCUGAIUFJKLUGUDUEUGAUFCZUEUDUGUJUEUFAMNABOPQRUDSAUFTUHAUAA
    UFUBKUC $.
    $([3-Apr-95]$)

  $( The successor of an element of an ordinal is a subset of it. $)
  ordsucssi $p |- ( Ord B -> ( A e. B -> suc A (_ B ) ) $=
    ( word wcel csuc wss wi wa wn tz7.25g imnan sylibr adantr wb ordssnel
    ordsuc sylanb sylibrd tz7.6 sylan exp31 pm2.43b ) BCZABDZAEZBFZUDUCUDUFGZUC
    UDUCUGACZUCUGUCUDHUHUCHUDBUEDZIZUFUHUDUJGZUCUHUDUIHIUKABJUDUIKLMUECUCUFUJNU
    HUEBOAPQRBASTUAUBUB $.
    $([21-Jun-98]$)

  $( The successor of an element of an ordinal is a subset of it. $)
  onsucssir $p |- ( A e. C -> ( suc A (_ B -> A e. B ) ) $=
    ( csuc wss wcel ssel sucidg syl5 comm ) ADZBEZACFZABFZLAKFNMKBAGACHIJ $.
    $([16-Sep-95]$)

  $( Membership is inherited by successors.  Generalization of Exercise
     9 of Takeuti and Zaring, p. 42. $)
  ordsucel $p |- ( Ord B -> ( A e. B <-> suc A e. suc B ) ) $=
    ( word wcel csuc wi wa cvv wb wss wceq wo ordssel ordsuc sylanb adantl
    ordsucssi ad2antrr onsucssir adantr impbid sucexbg elsucg sylbi 3bitr4d
    exp wn elisset sylibr pm5.21ni a1d pm2.61i biimpd tz7.6 sylan exp31
    pm2.43a pm2.43d biimprd ) BCZABDZAEZBEZDZUTVAVDVAUTVAVDFZUTVAUTVEACZUTVEUTV
    AGVFUTGZVAVDAHDZVGVAVDIZFVHVGVIVHVGGZVBBJZVBBDVBBKLZVAVDVGVKVLIZVHVBCZUTVMV
    FVBBMANZOPVJVAVKUTVAVKFVHVFABQRVHVKVAFVGABHSTUAVHVDVLIZVGVHVBHDZVPAUBZVBBHU
    CUDTUEUFVHUGVIVGVAVHVDABUHVDVQVHVBVCUHVRUIUJUKULZUMBAUNUOUPUQURUTVDVAVDUTVD
    VAFZUTVDUTVTVFUTVTUTVDGVGVAVDVSUSVCCZVDVFUTWAVDGVNVFVCVBUNVOUIBNOUOUPUQURUA
    $.
    $([22-Jun-98]$)

  $( Ordinal derived from its successor. $)
  orddif $p |- ( Ord A -> A = ( suc A \ { A } ) ) $=
    ( word csn cin c0 wceq csuc cdif orddisj disj3 cun df-suc difeq1 ax-mp
    undif2 eqtr4 cleq2i bitr4 sylib ) ABAACZDEFZAAGZTHZFZAIUAAATHZFUDATJUCUEAUC
    ATKZTHZUEUBUFFUCUGFALUBUFTMNATOPQRS $.
    $([20-May-98]$)

  $( An ordinal number is an element of ` On `.  From now on, "ordinal" will
     usually mean "ordinal number".  Here we derive some basic facts about
     ordinals.  Note that any inference ` ph ` with a hypothesis ` A e. On ` can
     be converted to a theorem of the form ` A e. On -> ph ` by using
     ~ dedth , ~ elimel , and ~ 0elon .  See, for example, ~ limsuc below. $)

  ${
    on.1 $e |- A e. On $.

    $( An ordinal is an ordinal class. $)
    onord $p |- Ord A $=
      ( con wcel word eloni ax-mp ) ACDAEBAFG $.
      $([11-Jun-94]$)

    $( An ordinal is a transitive class. $)
    ontrc $p |- Tr A $=
      ( word wtr onord ordtr ax-mp ) ACADABEAFG $.
      $([11-Jun-94]$)

    $( An ordinal is not a member of itself.  Theorem 7M(c) of Enderton,
       p. 192. $)
    oneirr $p |- -. A e. A $=
      ( word wcel wn onord ordeirr ax-mp ) ACAADEABFAGH $.
      $([11-Jun-94]$)

    $( An ordinal has no membership loop. $)
    onn2lp $p |- -. ( A e. B /\ B e. A ) $=
      ( word wcel wa wn onord ordn2lp ax-mp ) ADABEBAEFGACHABIJ $.
      $([11-Jun-94]$)

    $( A member of an ordinal is an ordinal.  Theorem 7M(a) of Enderton,
       p. 192. $)
    onel $p |- ( B e. A -> B e. On ) $=
      ( wcel con word onord wa cvv wi elisset elong biimprd syl tz7.6 syl5
      mpani pm2.43i ) BADZBEDZSAFZSTACGSBFZTUASHSBIDZUBTJBAKUCTUBBILMNABOPQR $.
      $([11-Jun-94]$)

    $( An ordinal is a subset of ` On `. $)
    onss $p |- A (_ On $=
      ( con wcel wss onsst ax-mp ) ACDACEBAFG $.
      $([11-Aug-94]$)

    $( A member of an ordinal is a subset of it. $)
    onelss $p |- ( B e. A -> B (_ A ) $=
      ( con wcel wss wi onelsst ax-mp ) ADEBAEBAFGCABHI $.
      $([11-Aug-94]$)

    $( Transitive law for ordinals.  Theorem 7M(b) of Enderton, p. 192. $)
    ontr1 $p |- ( ( B e. C /\ C e. A ) -> B e. A ) $=
      ( con wcel wa wi ontr1t ax-mp ) AEFBCFCAFGBAFHDABCIJ $.
      $([11-Aug-94]$)

    $( Transitive law for ordinals.  Exercise 4 of Takeuti and Zaring,
       p. 40. $)
    ontr2 $p |- ( ( B e. C /\ C (_ A ) -> B e. A ) $=
      ( wcel wss ssel comm imp ) BCEZCAFZBAEZKJLCABGHI $.
      $([11-Jun-94]$)

    $( See below for ~ ontr3 . $)

    $( Transitive law for ordinals. $)
    ontr4 $p |- ( ( B (_ C /\ C (_ A ) -> B (_ A ) $=
      ( wss sstr imp ) BCECAEBAEBCAFG $.
      $([11-Jun-94]$)

    $( An ordering law for ordinals. $)
    onssnel $p |- ( A (_ B -> -. B e. A ) $=
      ( wcel wss con word wn onel eloni ordeirr 3syl ssel comm mtod con2 ) BADZ
      ABEZQRBBDZQBFDBGSHABCIBJBKLRQSABBMNOP $.
      $([13-Jun-94]$)

    $( An ordering law for ordinals. $)
    onssnel2 $p |- ( B (_ A -> -. A e. B ) $=
      ( wss wcel oneirr ssel mtoi ) BADABEAAEACFBAAGH $.
      $([13-Jun-94]$)

    $( An element of an ordinal equals its intersection with the ordinal. $)
    onelin $p |- ( B e. A -> B = ( B i^i A ) ) $=
      ( wcel wss cin wceq onelss sseqin1 sylib ) BADBAEBBAFGABCHBAIJ $.
      $([11-Jun-94]$)

    $( An ordinal equals its union with any element. $)
    onelun $p |- ( B e. A -> A = ( A u. B ) ) $=
      ( wcel wss cun wceq onelss ssequn2 sylib ) BADBAEAABFGABCHBAIJ $.
      $([13-Jun-94]$)

    $( An ordinal includes its union. $)
    onuniss $p |- U. A (_ A $=
      ( wtr cuni wss ontrc df-tr mpbi ) ACADAEABFAGH $.
      $([11-Jun-94]$)

    $( The union of an ordinal is an ordinal.  Corollary 7N(d) of Enderton,
       p. 193. $)
    onuni $p |- U. A e. On $=
      ( cuni con wcel word wss onss tz7.19 ax-mp elisseti zfun2 elon mpbir ) AC
      ZDEOFZADGPABHAIJOAADBKLMN $.
      $([11-Jun-94]$)

    $( Ordinal membership in successor of a class. $)
    onsuci $p |- ( A e. suc B <-> ( A e. B \/ A = B ) ) $=
      ( con wcel csuc wceq wo wb elsucg ax-mp ) ADEABFEABEABGHICABDJK $.
      $([15-Sep-95]$)

    $( Membership in successor of an ordinal. $)
    onsucd2 $p |- ( B e. suc A <-> ( B e. A \/ B = A ) ) $=
      ( csuc wcel csn cun wo wceq df-suc eleq2i elun con elisseti elsnc2 bilor
      3bitr ) BADZEBAAFZGZEBAEZBSEZHUABAIZHRTBAJKBASLUBUCUABAAMCNOPQ $.
      $([12-Jun-94]$)

    $( An ordinal is an element of its successor. $)
    onelsuc $p |- A e. suc A $=
      ( con wcel csuc sucidg ax-mp ) ACDAAEDBACFG $.
      $([1-Sep-95]$)

    $( The successor of an ordinal is an ordinal.  Corollary 7N(c) of
       Enderton, p. 193. $)
    onsuc $p |- suc A e. On $=
      ( con wcel csuc tz7.24 ax-mp ) ACDAECDBAFG $.
      $([12-Jun-94]$)

    $( An ordinal is equal to the union of its successor. $)
    onunisuc $p |- A = U. suc A $=
      ( wtr csuc cuni wceq ontrc con elisseti unisuc mpbi ) ACAADEFABGAAHBIJK
      $.
      $([12-Jun-94]$)

    $( There is no set between an ordinal and its successor. $)
    onbtwn $p |- -. ( A e. B /\ B e. suc A ) $=
      ( con wcel csuc wa wn tz7.25 ax-mp ) ADEABEBAFEGHCABIJ $.
      $([16-Sep-95]$)

    ${
      on.2 $e |- B e. On $.

      $( Membership and proper subset relations are equivalent for ordinals. $)
      onelpss $p |- ( A e. B <-> ( A (_ B /\ -. A = B ) ) $=
        ( con wcel wss wceq wn wa wb onelpsst mp2an ) AEFBEFABFABGABHIJKCDABLM
        $.
        $([15-Sep-95]$)

      $( Subset is equivalent to membership or equality for ordinals. $)
      onssel $p |- ( A (_ B <-> ( A e. B \/ A = B ) ) $=
        ( con wcel wss wceq wo wb onsselt mp2an ) AEFBEFABGABFABHIJCDABKL $.
        $([15-Sep-95]$)

      $( Ordering law for ordinals. $)
      onelorss $p |- ( A e. B \/ B (_ A ) $=
        ( wcel wss wo wceq w3o word onord tz7.10 mp2an 3orass mpbi onssel
        orcom cleqcom biror 3bitr bilor mpbir ) ABEZBAFZGUCABHZBAEZGZGZUCUEUFIZ
        UHAJBJUIACKBDKABLMUCUEUFNOUDUGUCUDUFBAHZGUJUFGUGBADCPUFUJQUJUEUFBARSTUA
        UB $.
        $([12-Jun-94]$)

      $( Transitive law for ordinals.  Exercise 3 of Takeuti and Zaring,
         p. 40$)
      ontr3 $p |- ( ( B (_ C /\ C e. A ) -> B e. A ) $=
        ( wss wcel wi wa wceq wo word wb con onel eloni syl onord jctil
        ordssel biimpac ontr1 exp eleq1 biimprd jaoi pm2.43d imp ) BCFZCAGZBAGZ
        UIUJUKUIUJUJUKHZUIUJIBCGZBCJZKZULUJUIUOUJBLZCLZIUIUOMUJUQUPUJCNGUQACDOC
        PQBERSBCTQUAUMULUNUMUJUKABCDUBUCUNUKUJBCAUDUEUFQUCUGUH $.
        $([12-Jun-94]$)

      $( The union of two ordinals is an ordinal. $)
      onun $p |- ( A u. B ) e. On $=
        ( wcel wss wo cun con onelorss wceq onelun eleq1 mpbii syl ssequn1
        sylbi jaoi ax-mp ) BAEZABFZGABHZIEZBADCJTUCUATAUBKZUCABCLUDAIEUCCAUBIMN
        OUABUBKZUCABPUEBIEUCDBUBIMNQRS $.
        $([13-Jun-94]$)

      $( A  subset of an ordinal is a member of its successor. $)
      onsssuc $p |- ( A (_ B <-> A e. suc B ) $=
        ( wss wcel wceq wo csuc onssel onsuci bitr4 ) ABEABFABGHABIFABCDJABCKL
        $.
        $([16-Sep-95]$)

      $( The successor of a member of an ordinal is a subset of the ordinal.
         Generalization of Exercise 8 of Takeuti and Zaring, p. 40. $)
      onsucss $p |- ( A e. B <-> suc A (_ B ) $=
        ( wcel csuc wss word wi onord ordsucssi ax-mp con onsucssir impbi ) ABE
        ZAFBGZBHPQIBDJABKLAMEQPICABMNLO $.
        $([16-Sep-95]$)

      $( The successor of a member of a limit ordinal is also a member. $)
      onlimsuc $p |- ( ( Lim A /\ B e. A ) -> suc B e. A ) $=
        ( wlim wcel csuc wn wi wceq wss wo onsucss onsuc onssel df-or 3bitr
        biimp wa cuni limuni unieq onunisuc syl5req sylan9eqr eqimss onssnel
        3syl exp syl6 com3r ax-3 pm2.43d imp ) AEZBAFZBGZAFZUOUPURUOUPURHZUPHZI
        UPURIUPUSUOUTUPUSUQAJZUOUTIUPUSVAIZUPUQAKURVALVBBADCMUQABDNCOURVAPQRVAU
        OUTVAUOSABJABKUTUOVAAATZBAUAVAUQTVCBUQAUBBDUCUDUEABUFABCUGUHUIUJUKURUPU
        LUJUMUN $.
        $([14-Jun-94]$)

    $}

    $( The successor of a member of a limit ordinal is also a member. $)
    limsuc $p |- ( ( Lim x /\ A e. x ) -> suc A e. x ) $=
      ( cv wlim wcel csuc word c0 wceq wn cuni wa wi w3a df-lim 3anass bitr
      con cded limeq eleq2 bi2and bi2imd 0elon elimel onlimsuc dedth exp3a imp
      visset elon syl2anbr anabss5 sylbi ) ADZEZBUPFZBGZUPFZUQUPHZUPIJKZUPUPLJZ
      MZMZURUTNZUQVAVBVCOVEUPPVAVBVCQRZVAVDVFUPSFZUQVFVAVEVHUQVFVHUQURUTVHUQURM
      ZUTNVHUPITZEZBVJFZMZUSVJFZNUPIUPVJJZVIVMUTVNVOUQVKURVLUPVJUAUPVJBUBUCUPVJ
      USUBUDVJBUPISUEUFCUGUHUIUJUPAUKULVGUMUNUOUJ $.
      $([15-Jun-94]$)

    ${
      $d x A $.  $( ` x ` is dummy. $)
      $( An ordinal is a subset of the successor of its union. $)
      onsucuni $p |- A (_ suc U. A $=
        ( vx cuni csuc cv wcel wss elssuni wceq wo con word wb onel eloni
        onuni onord ordssel mpan2 3syl onsucd2 syl6bbr mpbid ssriv ) CAADZEZCFZA
        GZUHUFHZUHUGGZUHAIUIUJUHUFGUHUFJKZUKUIUHLGUHMZUJULNZAUHBOUHPUMUFMUNUFAB
        QZRUHUFSTUAUFUHUOUBUCUDUE $.
        $([13-Jun-94]$)
    $}

    $( An ordinal is either a limit or a successor. $)
    onuniorsuc $p |- ( A = U. A \/ A = suc U. A ) $=
      ( cuni wceq csuc wn wss wa wcel onuniss onuni onelpss biimpr mpan wi
      onbtwn imnan mpbir onsuc onelorss ori 3syl onsucuni jctil cleqcom bineg
      eqss 3imtr4 orri ) AACZDZAUJEZDZUJADZFZAULGZULAGZHUKFUMUOUQUPUOUJAIZAULIZ
      FZUQUJAGZUOURABJURVAUOHUJAABKZBLMNURUTOURUSHFUJAVBPURUSQRUSUQAULBUJVBSTUA
      UBABUCUDUKUNAUJUEUFAULUGUHUI $.
      $([13-Jun-94]$)

    ${
      $d x A $.
      $( A limit ordinal is not a successor. $)
      onuninsuc $p |- ( A = U. A <-> -. E. x A = suc x ) $=
        ( cuni wceq cv csuc wex wn wi wa wcel oneirr eleq1 biimparc visset
        sucid eleq2 mpbiri adantr id cun csn df-suc cleq2i unieq sylbi undist
        unisn uneq2i eqtr4 syl6eq wss con mpbii wtr word tz7.12 ordtr ax-mp
        trsuc mpan eloni syl df-tr sylib 3syl ssequn1 eqtr4d sylan9eqr sylan13
        mto imnan mpbir 19.23aiv con2 onuniorsuc ori elisseti zfun2 suceq
        cleq2d cla4ev con1 impbi ) BBDZEZBAFZGZEZAHZIWKWGWJWGIZAWJWLJWJWGKZIWMB
        BLZBCMWHBLZBWHEZWNWMWPWNWOBWHBNOWJWOWGWJWOWHWILWHAPZQBWIWHRSTWGWJBWFWHW
        GUAWJWFWHDZWHUBZWHWJWFWHWHUCZUBZDZWSWJBXAEWFXBEWIXABWHUDUEBXAUFUGXBWRWT
        DZUBWSWHWTUHWHXCWRWHWQUIUJUKULWJWRWHUMZWHWSEWJWIUNLZWHUNLZXDWJBUNLXECBW
        IUNNUOUNUPZXEXFUNUQXGURUNUSUTUNWHWQVAVBXFWHUPZXDXFWHUQXHWHVCWHUSVDWHVEV
        FVGWRWHVHVFVIVJVKVLWJWGVMVNVOVPWGWKWLBWFGZEZWKWGXJBCVQVRWJXJAWFBBUNCVSV
        TWHWFEWIXIBWHWFWAWBWCVDWDWE $.
        $([15-Apr-95]$)
    $}

  $}

  $( The successor of an ordinal contains the empty set. $)
  0elsuc $p |- ( Ord A -> (/) e. suc A ) $=
    ( word csuc c0 wcel ordsuc wceq wn nsuceq0 ord0eln0 mpbiri sylbi ) ABACZBZD
    MEZAFNOMDGHAIMJKL $.
    $([4-Apr-95]$)

  ${
    nlimsuc.1 $e |- A e. V $.
    $( A successor is not a limit ordinal. $)
    nlimsuc   $p |- -. Lim suc A $=
      ( word csuc wlim wn cuni wceq wcel ordsuc ordeirr sylbi eleq1 sucid wtr
      ordtr unisuc sylib eleq1d mpbii syl5bir comm mtod limuni nsyl c0 w3a
      3simp1 df-lim 3imtr4 con3 pm2.61i ) ACZADZEZFUMUNUNGZHZUOUMUQUNUNIZUMUNCZ
      URFAJZUNKLUQUMURUQURUPUNIZUMUNUPUNMUMAUNIVAABNUMAUPUNUMAOAUPHAPABQRSTUAUB
      UCUNUDUEUOUMUSUNUFHFZUQUGUSUOUMUSVBUQUHUNUIUTUJUKUL $.
      $([25-Mar-95]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Exercise 11 of Takeuti and Zaring, p. 40. $)
    unon $p |- On = U. On $=
      ( vx vy con cuni cv wcel csuc wa tz7.24 visset sucid jctil elunii syl
      wel wex eluni ontrt 19.23aiv sylbi impbi cleqri ) ACCDZAEZCFZUDUCFZUEUDUD
      GZFZUGCFZHUFUEUIUHUDIUDAJKLUDUGCMNUFABOBEZCFHZBPUEBUDCQUKUEBUDUJRSTUAUB
      $.
      $([24-Mar-95]$)
  $}

  $( The class of ordinal numbers is a limit ordinal. $)
  limon $p |- Lim On $=
    ( con wlim word c0 wceq wn cuni w3a tz7.12 onne0 unon 3pm3.2i df-lim mpbir
    ) ABACZADEFZAAGEZHOPQIJKLAMN $.
    $([24-Mar-95]$)

  ${
    $d x A $.
    $( A limit ordinal is not a successor. $)
    orduninsuc $p |- ( Ord A -> ( A = U. A <-> -. E. x A = suc x ) ) $=
      ( word con wcel wceq wo cuni cv csuc wex wn wb tz7.14 c0 cded id unieq
      cleq12d cleq1 biexdv binegd bi2bid 0elon elimel onuninsuc dedth unon cvv
      tz7.13 visset sucex eleq1 mpbiri mto nex 2th jaoi syl ) BCBDEZBDFZGBBHZFZ
      BAIZJZFZAKZLZMZBNUTVIVAUTVIUTBOPZVJHZFZVJVEFZAKZLZMBOBVJFZVCVLVHVOVPBVJVB
      VKVPQBVJRSVPVGVNVPVFVMABVJVETUAUBUCAVJBODUDUEUFUGVAVIDDHZFZDVEFZAKZLZMVRW
      AUHVSAVSDUIEZUJVSWBVEUIEVDAUKULDVEUIUMUNUOUPUQVAVCVRVHWAVABDVBVQVAQBDRSVA
      VGVTVAVFVSABDVETUAUBUCUNURUS $.
      $([15-Apr-95]$)
  $}


