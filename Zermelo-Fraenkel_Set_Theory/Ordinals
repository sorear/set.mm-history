$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Ord $. $( Ordinal predicate $)
  $c On $. $( The class of ordinals $)
  $c Lim $. $( Limit ordinal predicate $)
  $c suc $. $( Successor function (read:  'successor of') $)

  $( Extend the definition of a wff to include the ordinal predicate. $)
  word $a wff Ord A $.

  $( Extend the definition of a class to include the class of all ordinals. $)
  con $a class On $.

  $( Extend the definition of a wff to include the limit ordinal predicate. $)
  wlim $a wff Lim A $.

  $( Extend class notation to include the successor function. $)
  csuc $a class suc A $.

  $( Define the ordinal predicate, which is true for a class that is transitive
     and is well-ordered by the epsilon relation.  Similar to definition on
     p. 468, Bell and Machover. $)
  df-ord $a |- ( Ord A <-> ( Tr A /\ E We A ) ) $.

  $( Define class of all ordinals.  Definition 7.11 of Takeuti and
     Zaring, p. 38. $)
  df-on $a |- On = { x | Ord x } $.

  $( Define the limit ordinal predicate, which is true for a non-empty ordinal
     that is not a successor (i.e. that is the union of itself).  Compare Bell
     and Machover, p. 471 and Exercise (1), p. 42 of Takeuti and Zaring. $)
  df-lim $a |- ( Lim A <-> ( Ord A /\ -. A = (/) /\ A = U. A ) ) $.

  $( Define the successor of a class.  Definition 7.22 of Takeuti and Zaring,
     p. 41.  Our definition is a generalization to classes, although it
     is meaningless when classes are proper.  $)
  df-suc $a |- suc A = ( A u. { A } ) $.

  $( Equality theorem for the ordinal predicate. $)
  ordeq $p |- ( A = B -> ( Ord A <-> Ord B ) ) $=
      ( wceq wtr cep wwe wa word treq weeq2 bi2and df-ord 3bitr4g ) ABCZADZAEFZ
      GBDZBEFZGAHBHNOQPRABIABEJKALBLM $.
      $([17-Sep-93]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Membership in the ` On ` class is equivalent to the ordinal predicate
       (generalized). $)
    elong   $p |- ( A e. B -> ( A e. On <-> Ord A ) ) $=
      ( vx cv word con ordeq df-on vtoclabg ) CDZEAECAFBJAGCHI $.
      $([5-Jun-94]$)
  $}

  ${
    elon.1 $e |- A e. V $.
    $( Membership in the ` On ` class is equivalent to the ordinal
       predicate. $)
    elon   $p |- ( A e. On <-> Ord A ) $=
      ( cvv wcel con word wb elong ax-mp ) ACDAEDAFGACHBI $.
      $([5-Jun-94]$)
  $}

  $( Membership in the ` On ` class implies the ordinal predicate. $)
  eloni   $p |- ( A e. On -> Ord A ) $=
      ( con wcel word elong biimpd pm2.43i ) ABCZADZHHIABEFG $.
      $([5-Jun-94]$)

  $( Equality theorem for the limit predicate. $)
  limeq $p |- ( A = B -> ( Lim A <-> Lim B ) ) $=
      ( wceq word c0 wn cuni w3a wlim ordeq cleq1 binegd unieq cleq2d bitrd
      bi3and df-lim 3bitr4g ) ABCZADZAECZFZAAGZCZHBDZBECZFZBBGZCZHAIBISTUEUBUG
      UDUIABJSUAUFABEKLSUDAUHCUISUCUHAABMNABUHKOPAQBQR $.
      $([22-Apr-94]$)

  $( Epsilon well orders every ordinal.  Proposition 7.4, p. 36, of Takeuti
     and Zaring. $)
  ordwe $p |- ( Ord A -> E We A ) $=
      ( word wtr cep wwe wa df-ord biimp pm3.27d ) ABZACZADEZJKLFAGHI $.
      $([3-Apr-94]$)

  $( An ordinal class is transitive. $)
  ordtr $p |- ( Ord A -> Tr A ) $=
      ( word wtr cep wwe wa df-ord biimp pm3.26d ) ABZACZADEZJKLFAGHI $.
      $([3-Apr-94]$)

  $( Epsilon is well-founded on an ordinal class. $)
  ordfr $p |- ( Ord A -> E Fr A ) $=
      ( word cep wwe wfr ordwe wefr syl ) ABACDACEAFACGH $.
      $([22-Apr-94]$)

  $( An element of an ordinal is a subset of it. $)
  ordelss $p |- ( ( Ord A /\ B e. A ) -> B (_ A ) $=
      ( wtr wcel wss word trss imp ordtr sylan ) ACZBADZBAEZAFKLMABGHAIJ $.
      $([30-May-94]$)

  $( A transitive subclass of an ordinal is ordinal. $)
  trssord $p |- ( ( Tr A /\ A (_ B /\ Ord B ) -> Ord A ) $=
      ( wtr wss word wa cep wwe w3a wess imp ordwe sylan5 imlan 3anass df-ord
      3imtr4 ) ACZABDZBEZFZFRAGHZFRSTIAEUAUBRSBGHZUBTSUCUBABGJKBLMNRSTOAPQ $.
      $([29-May-94]$)

  $( No ordinal is a member of itself.  Theorem 2.2(i), p. 469, of Bell and
     Machover, generalized to classes. $)
  nordel $p |- ( Ord A -> -. A e. A ) $=
      ( word cep wfr wcel wn ordfr epnec syl ) ABACDAAEFAGAHI $.
      $([2-Jan-94]$)

  $( An ordinal cannot an element of one of its members.  Similar to first
     part of Theorem 2.2(vii), p. 469, of Bell and Machover. $)
  nordel2 $p |- ( Ord A -> -. ( A e. B /\ B e. A ) ) $=
      ( word wcel wa wtr wi ordtr trel syl nordel mtod ) ACZABDBADEZAADZMAFNOG
      AHAABIJAKL $.
      $([3-Apr-94]$)

  ${
    $d x R $.  $d x A $.  $d x B $.
    $( A subclass (possibly proper) of an ordinal has a minimal element.
       Proposition 7.5 of Takeuti and Zaring, p. 36. $)
    tz7.5 $p |- ( ( Ord A /\ ( B (_ A /\ -. B = (/) ) ) ->
               E. x ( x e. B /\ ( B i^i x ) = (/) ) ) $=
      ( cep wwe wss c0 wceq wn wa cv wcel cin wex word wefrc ordwe sylan ) BDEC
      BFCGHIJAKZCLCSMGHJANBOABCPBQR $.
      $([28-Apr-94]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( An element of an ordinal is ordinal.  Proposition 7.6 of Takeuti and
       Zaring, p. 36. $)
    tz7.6 $p |- ( ( Ord A /\ B e. A ) -> Ord B ) $=
      ( vx vz vy word wcel cv wa wi wceq cleq3 biland ordeq bi2imd wtr cep
      wwe wel wal w3a pm3.26 adantr ordtr trel3 syl 3anrot 3anass bitr3
      syl5ibr exp3a imp31 trel com23 adantarl pm3.27 3jca jca wetrep ordwe
      sylan exp pm2.43d 19.21aivv dftr2 sylibr wss trss wess comm syld imp
      df-ord vtoclg anabsi7 ) AFZBAGZBFZVPCHZAGZIZVSFZJVPVQIZVRJCBAVSBKZWAWCWBV
      RWDVTVQVPVSBALMVSBNOWAVSPZVSQRZIWBWAWEWFWADESZECSZIZDCSZJZETDTWEWAWKDEWAW
      IWJWAWIWKWAWIIZVPDHZAGZEHZAGZVTUAZIWKWLVPWQWAVPWIVPVTUBUCWLWNWPVTVPVTWIWN
      VPVTWIWNVPWGWHVTUAZWNVTWIIZVPAPZWRWNJAUDZAWMWOVSUEUFWRVTWGWHUAWSVTWGWHUGV
      TWGWHUHUIUJUKULWAWHWPWGVPVTWHWPVPWHVTWPVPWHVTWPVPWTWHVTIWPJXAAWOVSUMUFUKU
      NULUOWAVTWIVPVTUPUCUQURAQRZWQWKVPDECAUSAUTZVAUFVBVCVDDEVSVEVFVPVTWFVPVTVS
      AVGZWFVPWTVTXDJXAAVSVHUFVPXBXDWFJXCXDXBWFVSAQVIVJUFVKVLURVSVMVFVNVO $.
      $([23-Apr-94]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Proposition 7.7 of Takeuti and Zaring, p. 37. $)
    tz7.7 $p |- ( ( Ord A /\ Tr B ) ->
                ( ( B (_ A /\ -. B = A ) <-> B e. A ) ) $=
      ( vx vy word wtr wa wss wceq wn wcel wi cdif c0 cv cin wex cleq3 biimpd
      ordtr trss eldifi syl5 difin0ss comm syl6 syl adantr imp32 wel weq w3o
      cep wwe wecmpep ordwe ssel imp im2an syl2an adantalr biimpcd eldifn
      nsyli adantall adantl trel exp3a com23 exp adantld ecased exp44 com34
      imp31 ssrdv adantarr jca eqss sylibr syl13 19.23adv tz7.5 difss mpan21
      exp4b adantrd pm2.43i pssdifn0 syl7 exp4a pm2.43d imp3a wfr tz7.2 ordfr
      sylan impbid ) AEZBFZGZBAHZBAIJZGZBAKZXAXBXCXEXAXBXCXELXAXBXBXCXEXAXBABMZ
      NIJZXEXDXAXBXGXELZLZXAWSXIWTXAXBWSXHXAXBWSXGXEXAXBGZCOZXFKZXFXKPNIZGZCQZX
      EWSXGGXJXNXECXJXNXEXKBIZXKAKZXEXJXNGZXPXQXEXKBARSXRXKBHZBXKHZGXPXRXSXTXJX
      LXMXSXAXLXMXSLZLZXBWSYBWTWSAFZYBATZYCXLXKAHZYAYCXQYEXLAXKUAXKABUBZUCXMYEX
      SABXKUDUEUFUGUHUHUIXJXLXTXMXJXLGDBXKXAXBXLDOZBKZDCUJZLXAXBYHXLYIXAXBYHXLY
      IXAXBYHGZXLGZGYIDCUKZCDUJZWSYKYIYLYMULZWTAUMUNYGAKZXQGYNWSYKDCAUOAUPYJYOX
      LXQXBYHYOBAYGUQURYFUSUTVAYKYLJZXAYHXLYPXBYHXLYPYHYLXKBKZXLYLYHYQYGXKBRVBX
      KABVCZVDURVEVFWTYKYMJZWSWTYJXLYSWTYHXLYSLZXBWTYHYTWTYHGYMYQXLWTYHYMYQLWTY
      MYHYQWTYMYHYQBXKYGVGVHVIURYRVDVJVKUIVEVLVMVNVOVPVQVRXKBVSVTXNXQXJXLXQXMYF
      UHVFWAVJWBWSXFAHXGXOCAXFWCABWDWEUCWFVIWGWHBAWIWJWKWLWMWSXEXDLWTWSXEXDYCAU
      MWNZGXEXDWSABWOWSYCUVAYDAWPVRWQVJUHWR $.
      $([5-May-94]$)
  $}

  $( Corollary 7.8 of Takeuti and Zaring, p. 37. $)
  tz7.8 $p |- ( ( Ord A /\ Ord B ) ->
              ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
      ( word wcel wss wceq wn wa wb wtr tz7.7 ordtr sylan5 bicomd ancoms ) BCZA
      CZABDZABEABFGHZIPQHSRPAJSRIQBAKALMNO $.
      $([25-Nov-95]$)

  $( Subclass is equivalent to membership or equality for ordinals. $)
  ordssel $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
      ( word wa wss wcel wceq wo wn wi tz7.8 biimprd exp3a orcom df-or bitr
      syl6ibr pm3.26 syl6bi eqimss jctir jaob sylibr impbid ) ACBCDZABEZABFZAB
      GZHZUEUFUHIZUGJZUIUEUFUJUGUEUGUFUJDZABKZLMUIUHUGHUKUGUHNUHUGOPQUEUGUFJZUH
      UFJZDUIUFJUEUNUOUEUGULUFUMUFUJRSABTUAUGUFUHUBUCUD $.
      $([25-Nov-95]$)

  $( Proposition 7.9 of Takeuti and Zaring, p. 37. $)
  tz7.9 $p |- ( ( Ord A /\ Ord B ) -> Ord ( A i^i B ) ) $=
      ( word cin wtr wa wss trssord inss2 mp3an2 trin ordtr syl2an sylan
      anabss3 ) ACZBCZABDZCZREZQSPQFTRBGQSRBHABIJAEBETPQABKALBLMNO $.
      $([9-May-94]$)

  ${
    $d x y A $.  $d x y B $.
    $( The intersection of two ordinal numbers is an ordinal number. $)
    onin $p |- ( ( A e. On /\ B e. On ) -> ( A i^i B ) e. On ) $=
      ( con wcel wa cin word cvv wb pm3.26 zfausclg elong 3syl tz7.9 eloni
      syl2an mpbird ) ACDZBCDZEZABFZCDZUAGZTRUAHDUBUCIRSJABCKUAHLMAGBGUCRSABNAO
      BOPQ $.
      $([7-Apr-95]$)
  $}

  $( Proposition 7.10 of Takeuti and Zaring, p. 38. $)
  tz7.10 $p |- ( ( Ord A /\ Ord B ) -> ( A e. B \/ A = B \/ B e. A ) ) $=
      ( word wa wcel wceq wo w3o wss ordssel wb ancoms bi2ord cin wn inss1
      mpbii tz7.9 sylan anabss1 ord sseqin1 cleqcom bitr syl6ibr anabss4
      im2ord nordel syl elin incom cleq3i bilan bineg ianor sylib mpd mpbid
      df-3or orabs bilor or1r or4 3bitr4 sylibr ) ACZBCZDZABEZABFZGZBAEZBAFZGZG
      ZVIVJVLHZVHABIZBAIZGZVOVHVQVKVRVNABJVGVFVRVNKBAJLMVHABNZAEZOZBANZBEZOZGZV
      SVHWBVQWEVRVHWBVTAFZVQVHWAWGVFVGWAWGGZVTCZVFWHVHWIVFDVTAIWHVTAJABPQABRZST
      UAVQAVTFWGABUBAVTUCUDUEVHWEWCBFZVRVHWDWKVFVGWDWKGZWCCZVGWLVGVFDWMVGDWCBIW
      LWCBJBAPQBARSUFUAVRBWCFWKBAUBBWCUCUDUEUGVHVTVTEZOZWFVHWIWOWJVTUHUIWOWAWDD
      ZOWFWNWPWNWAVTBEZDWPVTABUJWQWDWAVTWCBABUKULUMUDUNWAWDUOUDUPUQURVPVKVLGZVO
      VIVJVLUSVIVLGZVJGWSVJVMGZGWRVOVJWTWSVJVJVJGWTVJUTVJVMVJABUCVAUDVAVIVJVLVB
      VIVJVLVMVCVDUDVE $.
      $([10-May-94]$)

  $( Trichotomy law for ordinals. $)
  ordssnel $p |- ( ( Ord A /\ Ord B ) -> ( A (_ B <-> -. B e. A ) ) $=
      ( word wa wss wcel wceq wo wn ordssel wi nordel2 imnan sylibr adantr
      cleq4 binegd nordel syl5bir comm adantl jaod w3o tz7.10 df-3or orcom
      df-or 3bitr sylib impbid bitrd ) ACZBCZDZABEABFZABGZHZBAFZIZABJUNUQUSUN
      UOUSUPULUOUSKZUMULUOURDIUTABLUOURMNOUMUPUSKULUPUMUSUPUSBBFZIUMUPURVAABBPQ
      BRSTUAUBUNUOUPURUCZUSUQKZABUDVBUQURHURUQHVCUOUPURUEUQURUFURUQUGUHUIUJUK
      $.
      $([25-Mar-95]$)

  $( Trichotomy law for ordinals. $)
  ordtri $p |- ( ( Ord A /\ Ord B ) ->
               ( A e. B <-> -. ( A = B \/ B e. A ) ) ) $=
      ( word wa wceq wcel wo wn wb wss ordssel cleqcom bilor orcom bitr syl6bb
      ordssnel bitr3d ancoms bicon2d ) ACZBCZDABEZBAFZGZABFZUBUAUEUFHZIUBUADZ
      BAJZUEUGUHUIUDBAEZGZUEBAKUKUDUCGUEUJUCUDBALMUDUCNOPBAQRST $.
      $([25-Nov-95]$)

  $( Trichotomy law for ordinals. $)
  ordtri2 $p |- ( ( Ord A /\ Ord B ) ->
               ( A = B <-> -. ( A e. B \/ B e. A ) ) ) $=
      ( word wa wceq wcel wo wn cleq4 binegd nordel syl5bi syl5bir im2and
      ioran syl6ibr comm w3o wi tz7.10 df-3or or1r df-or 3bitr sylib impbid )
      ACZBCZDZABEZABFZBAFZGZHZUJUIUNUJUIUKHZULHZDUNUJUGUOUHUPUJAAFZHUOUGUJUQUKA
      BAIJAKLUJUPBBFZHUHUJULURABBIJBKMNUKULOPQUIUKUJULRZUNUJSZABTUSUKUJGULGUMUJ
      GUTUKUJULUAUKUJULUBUMUJUCUDUEUF $.
      $([18-Oct-95]$)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( The ordinal class is founded.  This lemma is needed for
       ~tz7.12 in order to eliminate the need for the Axiom of Regularity. $)
    onfr $p |- E Fr On $=
      ( vx vz vy con cep wfr cv wss c0 wceq wn wa wel cin wex wi dfepfr weq
      a13b ineq2 cleq1d bi2and biimprd a4w exp comm a1d word wcel ssel visset
      elon syl6ib wtr ordfr ordtr jca zfauscl epfrc inss2 mpani inss1 ssi trss
      syl5 sseqin2 inass syl6eqr sylbi biimprcd sylan9 imp im2an exp32 pm2.43b
      com23 imp3a 19.22dv syl syl6 com3r pm2.61i 19.23adv nnull syl5ib mpgbir
      ) DEFAGZDHZWGIJKZLBAMZWGBGZNZIJZLZBOZPAABDQWHWIWOWHCAMZCOWOWIWHWPWOCWGCGZ
      NZIJZWHWPWOPZPWSWTWHWPWSWOWPWSWOWPWSLZWNBCBCRZWNXAXBWJWPWMWSBCASXBWLWRIWK
      WQWGTUAUBUCUDUEUFUGWHWPWSKZWOWHWPWQUHZXCWOPZWHWPWQDUIXDWGDWQUJWQCUKULUMXD
      WQEFZWQUNZLXEXDXFXGWQUOWQUPUQXFXCWKWRUIZWRWKNZIJZLZBOZXGWOXFWRWQHZXCXLXFX
      MXCLXLBWQWRWGWQAUKURUSUEWGWQUTZVAXGXKWNBXGXHXJWNXGXJXHWNXGXJXHWNPXGXJLZXH
      WNXHXOXHWNXHWJXOXHLWMWRWGWKWGWQVBVCXOXHWMXGXHWKWQHZXJWMXGBCMXPXHWQWKVDWRW
      QWKXNVCVEXPWMXJXPWLXIIXPWKWQWKNZJZWLXIJWKWQVFXRWLWGXQNXIWKXQWGTWGWQWKVGVH
      VIUAVJVKVLVMVNVOUEVPVQVRVKVSVTWAWBWCCWGWDWEVLWF $.
      $([17-May-94]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The ordinal class is an ordinal.  Proposition 7.12 of Takeuti and
       Zaring, p. 38, but without using the Axiom of Regularity. $)
    tz7.12 $p |- Ord On $=
      ( vx vy con word wtr cep wwe wa df-ord cv wcel wss wi dftr3 wel tz7.6
      visset elon sylanb exp syl6ibr ssrdv mpgbir wfr wbr weq w3o wral dfwe2
      onfr tz7.10 epel pm4.2 bi3or sylibr eloni syl2an rgen2 pm3.2i mpbir ) CDC
      EZCFGZHCIVAVBVAAJZCKZVCCLMAACNVDBVCCVDBAOZBJZDZVFCKZVDVEVGVCDZVEVGVDVCVFP
      VCAQRSTVFBQRUAUBUCVBCFUDZVCVFFUEZABUFZVFVCFUEZUGZBCUHACUHZHABCFUIVJVOUJVN
      ABCVIVGVNVDVHVIVGHABOZVLVEUGVNVCVFUKVKVPVLVLVMVEABULVLUMBAULUNUOVCUPVFUPU
      QURUSUTUSUT $.
      $([17-May-94]$)
  $}

  $( ` x ` is dummy. $)
  $( No set contains all ordinal numbers.  Proposition 7.13 of Takeuti and
       Zaring, p. 38, but without using the Axiom of Regularity.  Also known
       as the Burali-Forti paradox (Enderton, p. 194).  Note that Burali-Forti
       was one person, not two. $)
  tz7.13 $p |- -. On e. V $=
      ( con cvv wcel word elong tz7.12 mpbiri wn nordel ax-mp mto ) ABCZAACZLMA
      DZABEFGNMHAIFJK $.
      $([18-May-94]$)

  $( Corollary 7.14 of Takeuti and Zaring, p. 38. $)
  tz7.14 $p |- ( Ord A -> ( A e. On \/ A = On ) ) $=
      ( word con wcel wceq wo w3o tz7.10 tz7.12 mpan2 df-3or sylib ord cvv
      elisset tz7.13 mto mt3i ) ABZACDZACEZFZCADZSUBUCSTUAUCGZUBUCFSCBUDACHIJTU
      AUCKLMUCCNDCAOPQR $.
      $([18-May-94]$)

  $( Corollary 7.15 of Takeuti and Zaring, p. 38. $)
  tz7.15 $p |- ( Ord A -> A (_ On ) $=
      ( word con wcel wss wceq wn wa wb tz7.8 tz7.12 mpan2 pm3.26 syl6bi
      tz7.14 ord eqimss syl6 pm2.61d ) ABZACDZACEZTUAUBACFZGZHZUBTCBUAUEIACJK
      LUBUDMNTUAGUCUBTUAUCAOPACQRS $.
      $([18-May-94]$)

  $( An ordinal number is a subset of ` On `. $)
  onsst $p |- ( A e. On -> A (_ On ) $=
      ( con wcel word wss eloni tz7.15 syl ) ABCADABEAFAGH $.
      $([5-Jun-94]$)

  ${
    $d x A $.
    $( The Principle of Transfinite Induction.  Theorem 7.17 of Takeuti and
       Zaring, p. 39. $)
    tfi   $p |- ( ( A (_ On /\ A. x ( x e. On -> ( x (_ A -> x e. A ) ) ) ->
                A = On ) $=
      ( con wss cv wcel wi wal wa wceq cdif cin c0 wex wn onsst difin0ss comm
      syl syl4d a2i eldifi syl5 imp3a pm2.65 eldifn adantr mpi 19.20i 19.7
      sylib ssdif0 bineg word tz7.5 tz7.12 mpan difss sylbi nsyl2 imlan eqss
      sylibr ) BCDZAEZCFZVEBDZVEBFZGZGZAHZIVDCBDZIBCJVKVLVDVKVECBKZFZVMVELMJZIZ
      ANZVLVKVPOZAHVQOVJVRAVJVPVHGZVRVJVNVOVHVJVFVOVHGZVNVFVIVTVFVOVGVHVFVECDZV
      OVGGVEPVOWAVGCBVEQRSTUAVECBUBUCUDVSVPVHOZGVRVPVHUEVNWBVOVECBUFUGUHSUIVPAU
      JUKVLOVMMJZOZVQVLWCCBULUMVMCDZWDVQCUNWEWDIVQACVMUOUPUQCBURUQUSUTVABCVBVC
      $.
      $([10-Aug-94]$)
  $}

  ${
    $d x A $.
    tz7.17.1 $e |- A (_ On $.
    tz7.17.2 $e |- ( x e. On -> ( x (_ A -> x e. A ) ) $.
    $( The Principle of Transfinite Induction (inference rule).  Theorem 7.17
       of Takeuti and Zaring, p. 39. $)
    tz7.17   $p |- A = On $=
      ( con wss cv cdif wcel cin c0 wceq wa wex wn ssdif0 bineg word tz7.5
      tz7.12 mpan difss sylbi wi eldifi onsst difin0ss comm syl syld imp
      eldifn adantr pm2.65i nex mt3 eqssi ) BECEBFZAGZEBHZIZUTUSJKLZMZANZUROUTK
      LZOZVDURVEEBPQUTEFZVFVDERVGVFMVDAEUTSTUAEBUBUAUCVCAVCUSBIZVAVBVHVAUSEIZVB
      VHUDUSEBUEVIVBUSBFZVHVIUSEFZVBVJUDUSUFVBVKVJEBUSUGUHUIDUJUIUKVAVHOVBUSEBU
      LUMUNUOUPUQ $.
      $([31-Jul-94]$)
  $}

  ${
    $d y ph z $.  $d x y z $.  $( ` z ` is dummy. $)
    tfis.1 $e |- ( x e. On -> ( A. y ( y e. x -> ( ph x | y ) ) -> ph ) ) $.
    $( Transfinite Induction Schema.  Compare Exercise 25 of Enderton,
       p. 200. $)
    tfis   $p |- ( x e. On -> ph ) $=
      ( vz cv con wcel wi wa wb pm4.71 cab ssab wss wsb wel wal ax-17 ax17el
      hbsbv hbim hbal visset weq cleq3 a14b birimd bialdv sbequ12 bi2imd
      vtoclb df-ss hban bi2and abclf pm3.27 sylbi syl3 19.20i syl5 anc2li
      syl6ibr tz7.17 cleqcomi cleqabi mpbir ) BFZGHZAIVIVIAJZKVIALVJBGVJBMZGEVK
      ABGNEFZGHZVLVKOZVMABEPZJZVLVKHVMVNVOVMCEQZABCPZIZCRZVOVNVICBQZVRIZCRZAIZI
      VMVTVOIZIBVLVMWEBVMBSZVTVOBVSBCVQVRBCEBTABCUAZUBUCABEUAZUBUBEUDZBEUEZVIVM
      WDWEVHVLGUFZWJWCVTAVOWJWBVSCWJWAVQVRBECUGUHUIABEUJZUKUKDULVNVQCFZVKHZIZCR
      VTCVLVKUMWOVSCWNVRVQWNWMGHZVRJZVRVJWQBWMWPVRBWPBSWGUNCUDBCUEVIWPAVRVHWMGU
      FABCUJUOUPWPVRUQURUSUTURVAVBVJVPBVLVMVOBWFWHUNWIWJVIVMAVOWKWLUOUPVCVDVEVF
      VG $.
      $([1-Aug-94]$)
  $}

  ${
    $d y ph $.  $d x y $.
    tfis2f.1 $e |- ( ps -> A. x ps ) $.
    tfis2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2f.3 $e |- ( x e. On -> ( A. y ( y e. x -> ps ) -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2f   $p |- ( x e. On -> ph ) $=
      ( cv con wcel wel wi wal wsb ddel4 bilim bial syl5ib tfis ) ACDCHIJDCKZBL
      ZDMATACDNZLZDMGUCUADUBBTABCDEFOPQRS $.
      $([18-Aug-94]$)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x y $.
    tfis2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    tfis2.2 $e |- ( x e. On -> ( A. y ( y e. x -> ps ) -> ph ) ) $.
    $( Transfinite Induction Schema with implicit substitution. $)
    tfis2   $p |- ( x e. On -> ph ) $=
      ( ax-17 tfis2f ) ABCDBCGEFH $.
      $([18-Aug-94]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The union of a class of ordinals in an ordinal.  Proposition 7.19 of
       Takeuti and Zaring, p. 40. $)
    tz7.19 $p |- ( A (_ On -> Ord U. A ) $=
      ( vx vy cuni wtr con wss word trssord 3exp tz7.12 mpii cv wcel wi wal
      wel wa wex ssel eloni ordtr trss 3syl syl6 com23 ancr imp3a ssuni
      19.23adv eluni syl5ib 19.21aiv dftr3 sylibr tz7.6 exp visset elon
      syl6ibr syl ssrdv syl13 ) ADZEZVDFGZVDHZAFGZVEVFFHZVGVEVFVIVGVDFIJKLVHBM
      ZVDNZVJVDGZOZBPVEVHVMBVHBCQZCMZANZRZCSZVLVKVHVQVLCVHVQVJVOGZVPRZVLVHVNVPV
      TVHVNVPVSOVPVTOVHVPVNVSVHVPVOFNZVNVSOZAFVOTZWAVOHZVOEWBVOUAZVOUBVOVJUCUDU
      EUFVPVSUGUEUHVJVOAUIUEUJCVJAUKZULUMBVDUNUOVHBVDFVHVRVJFNZVKVHVQWGCVHVNVPW
      GVHVPVNWGVHVPWAVNWGOZWCWAWDWHWEWDVNVJHZWGWDVNWIVOVJUPUQVJBURUSUTVAUEUFUHU
      JWFULVBVC $.
      $([30-May-94]$)
  $}

  $( Transitive law for ordinals. $)
  ontrt $p |- ( ( A e. B /\ B e. On ) -> A e. On ) $=
      ( con wcel wa word wb elong adantl tz7.6 eloni sylan mpbird ancoms ) BCD
      ZABDZACDZOPEQAFZPQRGOABHIBFPROBAJBKLMN $.
      $([12-Aug-94]$)

  $( Transitive law for ordinals.  Theorem 7M(b) of Enderton, p. 192. $)
  ontr1t $p |- ( A e. On -> ( ( B e. C /\ C e. A ) -> B e. A ) ) $=
      ( con wcel word wtr wa wi eloni ordtr trel 3syl ) ADEAFAGBCECAEHBAEIAJAKA
      BCLM $.
      $([11-Aug-94]$)

  $( Relationship between membership and proper subset of an ordinal number. $)
  onelpsst $p |- ( ( A e. On /\ B e. On ) ->
               ( A e. B <-> ( A (_ B /\ -. A = B ) ) ) $=
      ( word wcel wss wceq wn wa wb con tz7.8 eloni syl2an ) ACBCABDABEABFGHIAJ
      DBJDABKALBLM $.
      $([15-Sep-95]$)

  $( Relationship between subset and membership of an ordinal number. $)
  onsselt $p |- ( ( A e. On /\ B e. On ) ->
                 ( A (_ B <-> ( A e. B \/ A = B ) ) ) $=
      ( word wss wcel wceq wo wb con ordssel eloni syl2an ) ACBCABDABEABFGHAIEB
      IEABJAKBKL $.
      $([15-Sep-95]$)

  $( An element of an ordinal number is a subset of the number. $)
  onelsst $p |- ( A e. On -> ( B e. A -> B (_ A ) ) $=
      ( con wcel word wtr wss wi eloni ordtr trss 3syl ) ACDAEAFBADBAGHAIAJABKL
      $.
      $([5-Jun-94]$)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( The intersection of a non-empty collection of ordinals is an ordinal.
       Compare Exercise 6 of Takeuti and Zaring, p. 44. $)
    onintOBS $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. On ) $=
      ( vx vy vz con wss c0 wceq wn wa cint wcel word wb cvv intex elong syl
      adantl wtr w3a wel cv wi wal ssel ontr1t syl6 exp4a com23 imp a2d
      19.21aiiv visset elint 3imtr4g exp imp3a 19.21aivv dftr2 sylibr adantr
      wex intss sstr onsst syl5 comm pm2.43d 19.23adv nnull syl5ib tz7.12 a1i
      3jca trssord mpbird ) AEFZAGHIZJZAKZELZWAMZVSWBWCNZVRVSWAOLWDAPWAOQRSVTWA
      TZWAEFZEMZUAWCVTWEWFWGVRWEVSVRBCUBZCUCZWALZJBUCZWALZUDZCUEBUEWEVRWMBCVRWH
      WJWLVRWHWJWLUDVRWHJZDUCZALZCDUBZUDZDUEWPBDUBZUDZDUEWJWLWNWRWTDWNWPWQWSVRW
      HWPWQWSUDZUDVRWPWHXAVRWPWHWQWSVRWPWOELWHWQJWSUDAEWOUFWOWKWIUGUHUIUJUKULUM
      DWIACUNUODWKABUNUOUPUQURUSBCWAUTVAVBVRVSWFVRWKALZBVCWFVSVRXBWFBVRXBWFVRXB
      WKELZXBWFUDAEWKUFXBXCWFXBWAWKFZXCWFUDWKAVDXDWKEFWFXCWAWKEVEWKVFVGRVHUHVIV
      JBAVKVLUKWGVTVMVNVOWAEVPRVQ $.
      $([7-Feb-97]$)
  $}

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( The intersection (infimum) of a non-empty ordinal class belongs to the
       class.  Compare Exercise 4 of Takeuti and Zaring, p. 45. $)
    ordint $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. A ) $=
      ( vx vy vz con wss c0 wceq wn cint wcel wi cv cin wa wex wel wal ssel
      word wb ordssnel eloni syl2an syl6bir exp sylan9 com4r imp a2d 19.21aiiv
      disj visset elint 3imtr4g imdistani sylan5 exp32 com4l imp32 ssrdv intss
      adantr adantl jca eqss sylibr cleq3d biimpd com34 pm2.43d imp3a 19.23adv
      tz7.5 tz7.12 mpan syl5 exp3a pm2.43i ) AEFZAGHIZAJZAKZVTWAWCLVTVTWAWCVTBM
      ZAKZAWDNGHZOZBPZWCVTWAOZVTWGWCBVTWEWFWCVTWEWFWCLVTWEWFWEWCVTWEWFWEWCLVTWG
      OZWEWCWJWDWBAWJWDWBFZWBWDFZOWDWBHWJWKWLWJCWDWBVTWEWFCBQZCMZWBKZLWMVTWEWFW
      OWMVTWEWFWOLZWMVTWDEKZOZWPVTWEOWMWROZDMZAKZDBQIZLZDRXACDQZLZDRWFWOWSXCXED
      WSXAXBXDWMWRXAXBXDLLWRXAXBWMXDVTXAWTEKZWQXBWMXDLZLZAEWTSWQXFXHWQXFOXBWDWT
      FZXGWDTWTTXIXBUAWQXFWDWTUBWDUCWTUCUDWDWTWNSUEUFUGUHUIUJUKDAWDULDWNACUMUNU
      OVTWEWQAEWDSUPUQURUSUTVAWGWLVTWEWLWFWDAVBVCVDVEWDWBVFVGVHVIURVJVKVLVMETWI
      WHBEAVNVOVPVQVRVSUI $.
      $([31-Jan-97]$)
  $}

  $( The intersection of a non-empty collection of ordinals is an ordinal.
     Compare Exercise 6 of Takeuti and Zaring, p. 44. $)
  onint $p |- ( ( A (_ On /\ -. A = (/) ) -> |^| A e. On ) $=
      ( con wss c0 wceq wn wa cint wcel wi ssel adantr ordint mpd ) ABCZADEFZGA
      HZAIZQBIZORSJPABQKLAMN $.
      $([29-Jan-97]$)

  $( The intersection of a collection of ordinals is its minimum. $)
  onmin $p |- ( ( A (_ On /\ B e. A ) -> -. B e. |^| A ) $=
      ( con wss wcel wa cint wn word wb c0 wceq onint noel2 sylan5 eloni syl
      ssel imp jca ordssnel intss adantl mpbid ) ACDZBAEZFZAGZBDZBUHEHZUGUHIZBI
      ZFUIUJJUGUKULUGUHCEZUKUEAKLHUMUFAMABNOUHPQUGBCEZULUEUFUNACBRSBPQTUHBUAQUF
      UIUEBAUBUCUD $.
      $([2-Feb-97]$)

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    onminex.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( If a wff is true for an ordinal, it is true for a minimum ordinal. $)
    onminex $p |- ( E. x e. On ph -> E. x e. On ( ph /\ A. y e. x -. ps ) ) $=
      ( cv con wcel wa wex wn wral wrex cab c0 wceq noel3 wel wi wal cint wss
      ordint ssab mpan ontrt onint sylan5 exp visset weq cleq3 bi2and abcl
      onmin sylbir pm2.03d syl6 pm2.43b 19.21aiv jca hbab1 hbint hbel ax-17
      hbim hbal hban cleq4 birimd bialdv cla4egf anabsi5 syl abid df-ral bi2an
      anass bitr3 biex sylib df-rex 3imtr4 ) CFZGHZAIZCJZWEABKZDWDLZIZIZCJZACGM
      WJCGMWGWFCNZOPKZWLWFCQWNWDWMHZDCRZWHSZDTZIZCJZWLWNWMUAZWMHZDFZXAHZWHSZDTZ
      IZWTWNXBXFWMGUBZWNXBWMUCACGUDZUEWNXEDWNXDWHXDWNXCGHZXEXDWNXJXDXAGHZXJWNXC
      XAUFXHWNXKWMUGXIUEUHUIXJBXDXJBXDKZXJBIZXCWMHZXLWFXMCXCDUJCDUKWEXJABWDXCGU
      LEUMUNXHXNXLWMXCUOXIUEUPUIUQURUSUTVAXBXFWTWSXGCDXAWMCDWMWFCDVBZVCZXBXFCCD
      DXAWMXPXOVDXECDXDWHCXPWHCVEVFVGVHWDXAPZWOXBWRXFWDXAWMULXQWQXEDXQWPXDWHWDX
      AXCVIVJVKUMVLVMVNWSWKCWSWFWIIWKWFWOWIWRWFCVOWHDWDVPVQWEAWIVRVSVTWAUPACGWB
      WJCGWBWC $.
      $([2-Feb-97]$)
  $}

  $( The empty set is an ordinal. $)
  ord0 $p |- Ord (/) $=
      ( c0 word wtr cep wwe wa df-ord tr0 we0 pm3.2i mpbir ) ABACZADEZFAGLMHDIJ
      K $.
      $([11-May-94]$)

  $( The empty set is an ordinal.  Corollary 7N(b) of Enderton, p. 193. $)
  0elon $p |- (/) e. On $=
      ( c0 con wcel word zfnull elon ord0 mpbir ) ABCADAEFGH $.
      $([17-Sep-93]$)

  $( A non-empty ordinal contains the empty set. $)
  ord0eln0 $p |- ( Ord A -> ( (/) e. A <-> -. A = (/) ) ) $=
      ( word c0 wcel wceq wn wi noel2 a1i wo wa ordtri bicon2d noel mpbiri
      ord0 mpan2 ord impbid ) ABZCADZACEZFZUAUCGTACHITUBUATCBZUBUAJZTUDKZUEAC
      DZFUFUGUEACLMANOPQRS $.
      $([25-Nov-95]$)

  $( The empty set is not a limit ordinal. $)
  nlim0 $p |- -. Lim (/) $=
      ( c0 wlim wceq word wn cuni w3a df-lim 3simp2 sylbi cleqid mt2 ) ABZAACZM
      ADZNEZAAFCZGPAHOPQIJAKL $.
      $([24-Mar-95]$)

  $( A limit ordinal is ordinal. $)
  limord $p |- ( Lim A -> Ord A ) $=
      ( wlim word c0 wceq wn cuni w3a df-lim 3simp1 sylbi ) ABACZADEFZAAGEZHLAI
      LMNJK $.
      $([4-May-95]$)

  $( A limit ordinal is its own supremum (union). $)
  limuni $p |- ( Lim A -> A = U. A ) $=
      ( wlim word c0 wceq wn cuni w3a df-lim 3simp3 sylbi ) ABACZADEFZAAGEZHNAI
      LMNJK $.
      $([4-May-95]$)

  $( A limit ordinal contains the empty set. $)
  0ellim $p |- ( Lim A -> (/) e. A ) $=
      ( wlim c0 wcel wceq wn word wb limord ord0eln0 syl limeq nlim0 mtbiri
      con2 mpbird ) ABZCADZACEZFZQAGRTHAIAJKSQSQCBACLMNOP $.
      $([15-May-94]$)

  $( A limit ordinal set is an ordinal number. $)
  limelon $p |- ( A e. B -> ( Lim A -> A e. On ) ) $=
      ( wcel con word wlim elong limord syl5bir ) ABCADCAEAFABGAHI $.
      $([4-May-95]$)

  $( The class of all ordinals in not empty. $)
  onne0 $p |- -. On = (/) $=
      ( c0 con wcel wceq wn noel2 0elon ax-mp ) ABCBADEBAFGH $.
      $([17-Sep-95]$)

  $( Equality of successors. $)
  suceq $p |- ( A = B -> suc A = suc B ) $=
      ( wceq csn cun csuc sneq uneq2 syl uneq1 eqtrd df-suc 3eqtr4g ) ABCZAADZ
      EZBBDZEZAFBFNPAQEZRNOQCPSCABGOQAHIABQJKALBLM $.
      $([30-Aug-93]$)

  $( Membership in a successor. $)
  elsuci $p |- ( A e. suc B -> ( A e. B \/ A = B ) ) $=
      ( csuc wcel csn wo wceq cun df-suc cleq4i elun bitr elsni imlor sylbi )
      ABCZDZABDZABEZDZFZRABGZFQABSHZDUAPUCABIJABSKLTUBRABMNO $.
      $([6-Jun-94]$)

  $( Membership in a successor. $)
  elsucg $p |- ( A e. C -> ( A e. suc B <-> ( A e. B \/ A = B ) ) ) $=
      ( wcel csn wo wceq csuc elsncg bilord cun df-suc cleq4i elun bitr
      syl5bb ) ACDZABDZABEZDZFZRABGZFABHZDZQTUBRABCIJUDABSKZDUAUCUEABLMABSNOP
      $.
      $([15-Sep-95]$)

  $( A proper class is its own successor. $)
  sucprc $p |- ( -. A e. V -> A = suc A ) $=
      ( cvv wcel wn c0 cun csn csuc wceq uneq2 snprc cleqcom 3imtr4 un0 df-suc
      3eqtr4g ) ABCDZAEFZAAGZFZAAHSEITRIQRTISEAJAKRTLMANAOP $.
      $([3-Apr-95]$)

  ${
    unisuc.1 $e |- A e. V $.
    $( A transitive set is equal to the union of its successor.  Generalization
       of Theorem 4E, p. 72, of Enderton. $)
    unisuc $p |- ( Tr A <-> A = U. suc A ) $=
      ( cuni wss cun wceq wtr csuc ssequn1 df-tr csn undist df-suc unieqi
      unisn uneq2i 3eqtr4 cleq2i 3bitr4 ) ACZADATAEZFAGAAHZCZFTAIAJUCUAAAAKZEZC
      TUDCZEUCUAAUDLUBUEAMNAUFTABOPQRS $.
      $([30-Aug-93]$)
  $}

  $( Part of Proposition 7.23 of Takeuti and Zaring, p. 41 (generalized). $)
  tz7.23b $p |- A (_ suc A $=
      ( csn cun csuc ssun1 df-suc sseqtr4 ) AAABZCADAHEAFG $.
      $([31-May-94]$)

  ${
    sucex.1 $e |- A e. V $.
    $( The successor of a set is a set. $)
    sucex   $p |- suc A e. V $=
      ( csuc csn cun cvv df-suc snex unex eqeltr ) ACAADZEFAGAKBAHIJ $.
      $([30-Aug-93]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The successor of a set is a set (generalization). $)
    sucexg   $p |- ( A e. B -> suc A e. V ) $=
      ( vx cv csuc cvv wcel wceq suceq cleq3d visset sucex vtoclg ) CDZEZFGAEZ
      FGCABNAHOPFNAIJNCKLM $.
      $([5-Jun-94]$)
  $}

  ${
    sucid.1 $e |- A e. V $.
    $( A set belongs to its successor. $)
    sucid   $p |- A e. suc A $=
      ( csn wcel csuc wo cun df-suc cleq4i elun bitr biimpr olci snid ax-mp )
      AACZDZAAEZDZAADZQSSTQFZSAAPGZDUARUBAAHIAAPJKLMABNO $.
      $([22-Jun-94]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Part of Proposition 7.23 of Takeuti and Zaring, p. 41 (generalized). $)
    sucidg $p |- ( A e. B -> A e. suc A ) $=
      ( vx cv csuc wcel wceq id suceq cleq34d visset sucid vtoclg ) CDZNEZFAAE
      ZFCABNAGZNAOPQHNAIJNCKLM $.
      $([25-Mar-95]$)
  $}

  $( No successor is empty. $)
  nsuceq0 $p |- -. suc A = (/) $=
      ( cvv wcel csuc c0 wceq wn cleq4 sucidg syl5bi comm noel mtoi sucprc
      cleq1d cleq3 zfnull mpbiri syl6bir pm2.16d pm2.43i pm2.61i ) ABCZADZEFZ
      GZUCUEAECZUEUCUGUEAUDCUGUCUDEAHABIJKALMUCGZUFUHUEUCUHUEAEFZUCUHAUDEANOUIU
      CEBCAEBPQRSTUAUB $.
      $([3-Apr-95]$)

  ${
    eqelsuc.1 $e |- A e. V $.
    $( A set belongs to the successor of an equal set. $)
    eqelsuc   $p |- ( A = B -> A e. suc B ) $=
      ( wceq csuc wcel suceq cleq4d sucid mpbii ) ABDZAAEZFABEZFKLMAABGHACIJ
      $.
      $([18-Aug-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    trsuc.1 $e |- B e. V $.
    $( A set whose sucessor belongs to a transitive class also belongs. $)
    trsuc   $p |- ( ( Tr A /\ suc B e. A ) -> B e. A ) $=
      ( vx wtr csuc wcel cv wceq wi wal trel exp3a csn elsn wo cun df-suc
      cleq4i elun bitr biimpr olci sylbir syl5 com23 19.21adv clel2 syl6ibr
      imp ) AEZBFZAGZBAGZUKUMDHZBIZUOAGZJZDKUNUKUMURDUKUPUMUQUKUOULGZUMUQJUPUKU
      SUMUQAUOULLMUPUOBNZGZUSDBOUOBGZVAUSUSVBVAPZUSUOBUTQZGVCULVDUOBRSUOBUTTUAU
      BUCUDUEUFUGDBACUHUIUJ $.
      $([21-Jun-94]$)
  $}

  $( A  subset of an ordinal is a member of its successor. $)
  onsssuct $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B <-> A e. suc B ) ) $=
      ( con wcel wa wss wceq wo csuc onsselt wb elsucg adantr bitr4d ) ACDZBCD
      ZEABFABDABGHZABIDZABJORQKPABCLMN $.
      $([15-Sep-95]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Proposition 7.24 of Takeuti and Zaring, p. 41. $)
    tz7.24 $p |- ( A e. On -> suc A e. On ) $=
      ( vx con wcel csuc word cvv wb sucexg elong syl wtr wss trssord 3exp
      tz7.12 mpii cv wi wal csn wo onelsst wceq elsn eqimss sylbi a1i im2ord
      cun df-suc cleq4i elun bitr orabs 3imtr4g sstr tz7.23b mpi syl6 19.21aiv
      dftr3 sylibr wa onsst snssi jca unss sseq1i syl13 mpbird ) ACDZAEZCDZVMF
      ZVLVMGDVNVOHACIVMGJKVMLZVMCMZVOVLVPVQCFZVOVPVQVRVOVMCNOPQVLBRZVMDZVSVMMZS
      ZBTVPVLWBBVLVTVSAMZWAVLVSADZVSAUAZDZUBZWCWCUBVTWCVLWDWCWFWCAVSUCWFWCSVLWF
      VSAUDWCBAUEVSAUFUGUHUIVTVSAWEUJZDWGVMWHVSAUKZULVSAWEUMUNWCUOUPWCAVMMWAVSA
      VMUQAURUSUTVABVMVBVCVLWHCMZVQVLACMZWECMZVDWJVLWKWLAVEACVFVGAWECVHVCVMWHCW
      IVIVCVJVK $.
      $([6-Jun-94]$)
  $}

  $( Proposition 7.25 of Takeuti and Zaring, p. 41. $)
  tz7.25 $p |- ( A e. On -> -. ( A e. B /\ B e. suc A ) ) $=
      ( con wcel word csuc wa wn eloni wo nordel2 nordel jca ioran sylibr wceq
      elsuci imlan andi sylib cleq4 biimpac imlor syl nsyl ) ACDAEZABDZBAFDZGZH
      AIUFUGBADZGZAADZJZUIUFUKHZULHZGUMHUFUNUOABKALMUKULNOUIUKUGBAPZGZJZUMUIUGU
      JUPJZGURUHUSUGBAQRUGUJUPSTUQULUKUPUGULBAAUAUBUCUDUEUD $.
      $([9-Jun-94]$)

  $( The successor of an element of an ordinal is a subset of it. $)
  onsucssi $p |- ( B e. On -> ( A e. B -> suc A (_ B ) ) $=
      ( con wcel csuc wss wi wa wn tz7.25 imnan sylibr adantr word wb ordssnel
      eloni sylan tz7.24 syl2an sylibrd ontrt exp31 pm2.43d pm2.43b ) BCDZABDZA
      EZBFZUGUFUGUIGZUGUFUFUJACDZUFUJUGUFHUKUFHUGBUHDZIZUIUKUGUMGZUFUKUGULHIUNA
      BJUGULKLMUHCDZBNZUIUMOZUKUFUHNUPUQUOUHBPUHQRASBQTUAABUBRUCUDUE $.
      $([16-Sep-95]$)

  $( The successor of an element of an ordinal is a subset of it. $)
  onsucssir $p |- ( A e. C -> ( suc A (_ B -> A e. B ) ) $=
      ( csuc wss wcel ssel sucidg syl5 comm ) ADZBEZACFZABFZLAKFNMKBAGACHIJ $.
      $([16-Sep-95]$)

  $( An ordinal number is an element of ` On `.  From now on, "ordinal" will
     usually mean "ordinal number".  Here we derive some basic facts about
     ordinals.  Note that any inference ` ph ` with a hypothesis ` A e. On ` can
     be converted to a theorem of the form ` A e. On -> ph ` by using
     ~clded2 , ~clded3 , and ~0elon .  See, for example, ~limsuc below. $)

  ${
    on.1 $e |- A e. On $.

    $( An ordinal is an ordinal class. $)
    onord $p |- Ord A $=
      ( con wcel word eloni ax-mp ) ACDAEAFBG $.
      $([11-Jun-94]$)

    $( An ordinal is a transitive class. $)
    ontrc $p |- Tr A $=
      ( word wtr ordtr onord ax-mp ) ACADAEABFG $.
      $([11-Jun-94]$)

    $( An ordinal is not a member of itself.  Theorem 7M(c) of Enderton,
       p. 192. $)
    onnel $p |- -. A e. A $=
      ( word wcel wn nordel onord ax-mp ) ACAADEAFABGH $.
      $([11-Jun-94]$)

    $( An ordinal has no membership loop. $)
    onnel2 $p |- -. ( A e. B /\ B e. A ) $=
      ( word wcel wa wn nordel2 onord ax-mp ) ADABEBAEFGABHACIJ $.
      $([11-Jun-94]$)

    $( A member of an ordinal is an ordinal.  Theorem 7M(a) of Enderton,
       p. 192. $)
    onel $p |- ( B e. A -> B e. On ) $=
      ( wcel con word wa cvv wi elisset elong biimprd syl tz7.6 syl5 onord
      mpani pm2.43i ) BADZBEDZSAFZSTSBFZTUASGSBHDZUBTIBAJUCTUBBHKLMABNOACPQR $.
      $([11-Jun-94]$)

    $( An ordinal is a subset of ` On `. $)
    onss $p |- A (_ On $=
      ( con wcel wss onsst ax-mp ) ACDACEAFBG $.
      $([11-Aug-94]$)

    $( A member of an ordinal is a subset of it. $)
    onelss $p |- ( B e. A -> B (_ A ) $=
      ( con wcel wss wi onelsst ax-mp ) ADEBAEBAFGABHCI $.
      $([11-Aug-94]$)

    $( Transitive law for ordinals.  Theorem 7M(b) of Enderton, p. 192. $)
    ontr1 $p |- ( ( B e. C /\ C e. A ) -> B e. A ) $=
      ( con wcel wa wi ontr1t ax-mp ) AEFBCFCAFGBAFHABCIDJ $.
      $([11-Aug-94]$)

    $( Transitive law for ordinals.  Exercise 4 of Takeuti and Zaring,
       p. 40. $)
    ontr2 $p |- ( ( B e. C /\ C (_ A ) -> B e. A ) $=
      ( wcel wss ssel comm imp ) BCEZCAFZBAEZKJLCABGHI $.
      $([11-Jun-94]$)

    $( See below for ~ontr3 . $)

    $( Transitive law for ordinals. $)
    ontr4 $p |- ( ( B (_ C /\ C (_ A ) -> B (_ A ) $=
      ( wss sstr imp ) BCECAEBAEBCAFG $.
      $([11-Jun-94]$)

    $( An ordering law for ordinals. $)
    onssnel $p |- ( A (_ B -> -. B e. A ) $=
      ( wcel wss ssel comm con word wn onel eloni nordel 3syl mtod con2 ) BADZ
      ABEZQRBBDZRQSABBFGQBHDBISJABCKBLBMNOP $.
      $([13-Jun-94]$)

    $( An ordering law for ordinals. $)
    onssnel2 $p |- ( B (_ A -> -. A e. B ) $=
      ( wss wcel ssel onnel mtoi ) BADABEAAEBAAFACGH $.
      $([13-Jun-94]$)

    $( An element of an ordinal equals its intersection with the ordinal. $)
    onelin $p |- ( B e. A -> B = ( B i^i A ) ) $=
      ( wcel wss cin wceq onelss sseqin1 sylib ) BADBAEBBAFGABCHBAIJ $.
      $([11-Jun-94]$)

    $( An ordinal equals its union with any element. $)
    onelun $p |- ( B e. A -> A = ( A u. B ) ) $=
      ( wcel wss cun wceq onelss ssequn2 sylib ) BADBAEAABFGABCHBAIJ $.
      $([13-Jun-94]$)

    $( An ordinal includes its union. $)
    onuniss $p |- U. A (_ A $=
      ( wtr cuni wss df-tr ontrc mpbi ) ACADAEAFABGH $.
      $([11-Jun-94]$)

    $( The union of an ordinal is an ordinal.  Corollary 7N(d) of Enderton,
       p. 193. $)
    onuni $p |- U. A e. On $=
      ( cuni con wcel word elisseti zfun2 elon wss tz7.19 onss ax-mp mpbir ) AC
      ZDEOFZOAADBGHIADJPAKABLMN $.
      $([11-Jun-94]$)

    $( Ordinal membership in successor of a class. $)
    onsuci $p |- ( A e. suc B <-> ( A e. B \/ A = B ) ) $=
      ( con wcel csuc wceq wo wb elsucg ax-mp ) ADEABFEABEABGHIABDJCK $.
      $([15-Sep-95]$)

    $( Membership in successor of an ordinal. $)
    onsucd2 $p |- ( B e. suc A <-> ( B e. A \/ B = A ) ) $=
      ( csuc wcel csn cun wo wceq df-suc cleq4i elun con elisseti elsnc2 bilor
      3bitr ) BADZEBAAFZGZEBAEZBSEZHUABAIZHRTBAJKBASLUBUCUABAAMCNOPQ $.
      $([12-Jun-94]$)

    $( An ordinal is an element of its successor. $)
    onelsuc $p |- A e. suc A $=
      ( con wcel csuc sucidg ax-mp ) ACDAAEDACFBG $.
      $([1-Sep-95]$)

    $( The successor of an ordinal is an ordinal.  Corollary 7N(c) of
       Enderton, p. 193. $)
    onsuc $p |- suc A e. On $=
      ( con wcel csuc tz7.24 ax-mp ) ACDAECDAFBG $.
      $([12-Jun-94]$)

    $( An ordinal is equal to the union of its successor. $)
    onunisuc $p |- A = U. suc A $=
      ( wtr csuc cuni wceq con elisseti unisuc ontrc mpbi ) ACAADEFAAGBHIABJK
      $.
      $([12-Jun-94]$)

    $( There is no set between an ordinal and its successor. $)
    onbtwn $p |- -. ( A e. B /\ B e. suc A ) $=
      ( con wcel csuc wa wn tz7.25 ax-mp ) ADEABEBAFEGHABICJ $.
      $([16-Sep-95]$)

    ${
      on.2 $e |- B e. On $.

      $( Membership and proper subset relations are equivalent for ordinals. $)
      onelpss $p |- ( A e. B <-> ( A (_ B /\ -. A = B ) ) $=
      ( con wcel wss wceq wn wa wb onelpsst mp2an ) AEFBEFABFABGABHIJKABLCDM $.
      $([15-Sep-95]$)

      $( Subset is equivalent to membership or equality for ordinals. $)
      onssel $p |- ( A (_ B <-> ( A e. B \/ A = B ) ) $=
      ( con wcel wss wceq wo wb onsselt mp2an ) AEFBEFABGABFABHIJABKCDL $.
      $([15-Sep-95]$)

      $( Ordering law for ordinals. $)
      onelorss $p |- ( A e. B \/ B (_ A ) $=
      ( wcel wss wo wceq onssel orcom cleqcom biror 3bitr bilor w3o 3orass
      word tz7.10 onord mp2an mpbi mpbir ) ABEZBAFZGUCABHZBAEZGZGZUDUGUCUDUFBAH
      ZGUIUFGUGBADCIUFUIJUIUEUFBAKLMNUCUEUFOZUHUCUEUFPAQBQUJABRACSBDSTUAUB $.
      $([12-Jun-94]$)

      $( Transitive law for ordinals.  Exercise 3 of Takeuti and Zaring,
         p. 40$)
      ontr3 $p |- ( ( B (_ C /\ C e. A ) -> B e. A ) $=
      ( wss wcel wi wa wceq wo word wb con onel eloni syl onord jctil ordssel
      biimpac ontr1 exp cleq3 biimprd jaoi pm2.43d imp ) BCFZCAGZBAGZUIUJUKUI
      UJUJUKHZUIUJIBCGZBCJZKZULUJUIUOUJBLZCLZIUIUOMUJUQUPUJCNGUQACDOCPQBERSBCTQ
      UAUMULUNUMUJUKABCDUBUCUNUKUJBCAUDUEUFQUCUGUH $.
      $([12-Jun-94]$)

      $( The union of two ordinals is an ordinal. $)
      onun $p |- ( A u. B ) e. On $=
      ( wcel wss wo cun con wceq onelun cleq3 mpbii syl ssequn1 sylbi jaoi
      onelorss ax-mp ) BAEZABFZGABHZIEZTUCUATAUBJZUCABCKUDAIEUCAUBILCMNUABUBJZU
      CABOUEBIEUCBUBILDMPQBADCRS $.
      $([13-Jun-94]$)

      $( A  subset of an ordinal is a member of its successor. $)
      onsssuc $p |- ( A (_ B <-> A e. suc B ) $=
      ( wss wcel wceq wo csuc onssel onsuci bitr4 ) ABEABFABGHABIFABCDJABCKL $.
      $([16-Sep-95]$)

      $( The successor of a member of an ordinal is a subset of the ordinal.
         Generalization of Exercise 8 of Takeuti and Zaring, p. 40. $)
      onsucss $p |- ( A e. B <-> suc A (_ B ) $=
      ( wcel csuc wss con wi onsucssi ax-mp onsucssir impbi ) ABEZAFBGZBHENOIAB
      JDKAHEONIABHLCKM $.
      $([16-Sep-95]$)

      $( Membership is inherited by successors.  Generalization of Exercise
         9 of Takeuti and Zaring, p. 42. $)
      onsucel $p |- ( A e. B <-> suc A e. suc B ) $=
      ( wcel csuc wss onsucss onsuc onsssuc bitr ) ABEAFZBGLBFEABCDHLBACIDJK $.
      $([13-Jun-94]$)

      $( The successor of a member of a limit ordinal is also a member. $)
      onlimsuc $p |- ( ( Lim A /\ B e. A ) -> suc B e. A ) $=
      ( wlim wcel csuc wn wi wceq wss wo onsucss onsuc onssel df-or 3bitr
      biimp wa cuni limuni unieq onunisuc syl5eq cleqcomd sylan9req eqimss
      onssnel 3syl exp syl6 com3r ax-3 pm2.43d imp ) AEZBAFZBGZAFZUPUQUSUPUQUS
      HZUQHZIUQUSIUQUTUPVAUQUTURAJZUPVAIUQUTVBIZUQURAKUSVBLVCBADCMURABDNCOUSVBP
      QRVBUPVAVBUPSABJABKVAUPVBAATZBAUAVBBVDVBURTVDBURAUBBDUCUDUEUFABUGABCUHUIU
      JUKULUSUQUMUKUNUO $.
      $([14-Jun-94]$)

    $}

    ${
      $d x y $.  $( ` y ` is dummy. $)
      $( The successor of a member of a limit ordinal is also a member. $)
      limsuc $p |- ( ( Lim x /\ A e. x ) -> suc A e. x ) $=
      ( vy cv wlim wcel csuc word c0 wceq wn cuni wa wi w3a df-lim 3anass bitr
      con wel cab limeq cleq4 bi2and bi2imd 0elon clded2 onlimsuc clded3
      exp3a imp sylan5br visset elon sylanbr anabss5 sylbi ) AEZFZBUSGZBHZUSGZU
      TUSIZUSJKLZUSUSMKZNZNZVAVCOZUTVDVEVFPVHUSQVDVEVFRSZVDVGVIUSTGZVHVIVDVKUTV
      IVHVKUTVIVKUTVAVCUTVANZVCODAUAVKNDUBZFZBVMGZNZVBVMGZODUSTUSVMKZVLVPVCVQVR
      UTVNVAVOUSVMUCUSVMBUDUEUSVMVBUDUFVMBDUSTUGUHCUIUJUKULVJUMUSAUNUOUPUQURUL
      $.
      $([15-Jun-94]$)
    $}

    ${
      $d x A $.  $( ` x ` is dummy. $)
      $( An ordinal is a subset of the successor of its union. $)
      onsucuni $p |- A (_ suc U. A $=
      ( vx cuni csuc cv wcel wss wceq wo con word wb onel eloni ordssel onuni
      onord mpan2 3syl onsucd2 syl6bbr elssuni mpbid ssri ) CAADZEZCFZAGZUHUFH
      ZUHUGGZUIUJUHUFGUHUFIJZUKUIUHKGUHLZUJULMZAUHBNUHOUMUFLUNUHUFPUFABQZRSTUFU
      HUOUAUBUHAUCUDUE $.
      $([13-Jun-94]$)
    $}

    $( An ordinal is either a limit or a successor. $)
    onuniorsuc $p |- ( A = U. A \/ A = suc U. A ) $=
      ( cuni wceq csuc wn wss wa wcel onuni onelpss biimpr onuniss mpan wi
      imnan onbtwn mpbir onsuc onelorss ori 3syl onsucuni jctil cleqcom bineg
      eqss 3imtr4 orri ) AACZDZAUJEZDZUJADZFZAULGZULAGZHUKFUMUOUQUPUOUJAIZAULIZ
      FZUQUJAGZUOURURVAUOHUJAABJZBKLABMNURUTOURUSHFURUSPUJAVBQRUSUQAULBUJVBSTUA
      UBABUCUDUKUNAUJUEUFAULUGUHUI $.
      $([13-Jun-94]$)

    ${
      $d x A $.
      $( A limit ordinal is not a successor. $)
      onuninsuc $p |- ( A = U. A <-> -. E. x A = suc x ) $=
      ( cuni wceq cv csuc wex wn wi wa imnan wcel cleq4 visset sucid mpbiri
      adantr id cun csn df-suc cleq2i unieq sylbi undist unisn uneq2i eqtr4
      syl6eq wss con cleq3 mpbii wtr trsuc word ordtr tz7.12 ax-mp mpan eloni
      syl df-tr sylib 3syl ssequn1 eqtr4d sylan9req jca biimparc onnel mto
      mpbir 19.23aiv con2 onuniorsuc ori elisseti zfun2 suceq cleq2d cla4ev
      con1 impbi ) BBDZEZBAFZGZEZAHZIWKWGWJWGIZAWJWLJWJWGKZIWJWGLWMBBMZWMWHBMZB
      WHEZKWNWMWOWPWJWOWGWJWOWHWIMBWIWHNWHAOZPQRWGWJBWFWHWGSWJWFWHDZWHTZWHWJWFW
      HWHUAZTZDZWSWJBXAEWFXBEWIXABWHUBUCBXAUDUEXBWRWTDZTWSWHWTUFWHXCWRWHWQUGUHU
      IUJWJWRWHUKZWHWSEWJWIULMZWHULMZXDWJBULMXEBWIULUMCUNULUOZXEXFULWHWQUPULUQX
      GULURUSUTVAXFWHUOZXDXFWHUQXHWHVBWHURVCWHVDVEVFWRWHVGVEVHVIVJWPWNWOBWHBUMV
      KVCBCVLVMVNVOVPWGWKWLBWFGZEZWKWGXJBCVQVRWJXJAWFBBULCVSVTWHWFEWIXIBWHWFWAW
      BWCVCWDWE $.
      $([15-Apr-95]$)
    $}

  $}

  $( The successor of an ordinal is ordinal. $)
  ordsuc $p |- ( Ord A <-> Ord suc A ) $=
      ( cvv wcel word csuc wb con elong tz7.24 eloni syl syl6bir tz7.6 exp
      sucidg syl5 comm impbid wn wceq sucprc ordeq pm2.61i ) ABCZADZAEZDZFZUDU
      EUGUDUEAGCZUGABHUIUFGCUGAIUFJKLUGUDUEUGAUFCZUEUDUGUJUEUFAMNABOPQRUDSAUFTU
      HAUAAUFUBKUC $.
      $([3-Apr-95]$)

  $( The successor of an ordinal contains the empty set. $)
  0elsuc $p |- ( Ord A -> (/) e. suc A ) $=
      ( word csuc c0 wcel ordsuc wceq wn ord0eln0 nsuceq0 mpbiri sylbi ) ABACZB
      ZDMEZAFNOMDGHMIAJKL $.
      $([4-Apr-95]$)

  ${
    nlimsuc.1 $e |- A e. V $.
    $( A successor is not a limit ordinal. $)
    nlimsuc   $p |- -. Lim suc A $=
      ( word csuc wlim wn cuni wceq wcel cleq3 wtr ordtr unisuc sylib cleq3d
      sucid mpbii syl5bir comm ordsuc nordel sylbi mtod limuni nsyl c0 w3a
      3simp1 df-lim 3imtr4 con3 pm2.61i ) ACZADZEZFUMUNUNGZHZUOUMUQUNUNIZUQUMUR
      UQURUPUNIZUMUNUPUNJUMAUNIUSUMAUPUNUMAKAUPHALABMNOABPQRSUMUNCZURFATZUNUAUB
      UCUNUDUEUOUMUTUNUFHFZUQUGUTUOUMUTVBUQUHUNUIVAUJUKUL $.
      $([25-Mar-95]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Exercise 11 of Takeuti and Zaring, p. 40. $)
    unon $p |- On = U. On $=
      ( vx vy con cuni cv wcel csuc wa tz7.24 visset sucid jctil elunii syl
      wel wex eluni ontrt 19.23aiv sylbi impbi cleqri ) ACCDZAEZCFZUDUCFZUEUDUD
      GZFZUGCFZHUFUEUIUHUDIUDAJKLUDUGCMNUFABOBEZCFHZBPUEBUDCQUKUEBUDUJRSTUAUB
      $.
      $([24-Mar-95]$)
  $}

  $( The class of ordinal numbers is a limit ordinal. $)
  limon $p |- Lim On $=
      ( con wlim word c0 wceq wn cuni w3a df-lim tz7.12 onne0 unon 3pm3.2i
      mpbir ) ABACZADEFZAAGEZHAIOPQJKLMN $.
      $([24-Mar-95]$)

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( A limit ordinal is not a successor. $)
    orduninsuc $p |- ( Ord A -> ( A = U. A <-> -. E. x A = suc x ) ) $=
      ( vy word con wcel wceq wo cuni cv csuc wex wn wb tz7.14 wa cab id unieq
      cleq12d cleq1 biexdv binegd bi2bid 0elon clded2 onuninsuc clded3
      unon cvv cleq3 visset sucex mpbiri tz7.13 mto nex 2th jaoi syl ) BDBEFZBE
      GZHBBIZGZBAJZKZGZALZMZNZBOVAVJVBVJCJBFVAPCQZVKIZGZVKVFGZALZMZNCBEBVKGZVDV
      MVIVPVQBVKVCVLVQRBVKSTVQVHVOVQVGVNABVKVFUAUBUCUDAVKCBEUEUFUGUHVBVJEEIZGZE
      VFGZALZMZNVBVDVSVIWBVBBEVCVRVBRBESTVBVHWAVBVGVTABEVFUAUBUCUDVSWBUIVTAVTEU
      JFZVTWCVFUJFEVFUJUKVEAULUMUNUOUPUQURUNUSUT $.
      $([15-Apr-95]$)
  $}


