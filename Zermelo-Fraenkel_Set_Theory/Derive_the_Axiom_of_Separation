$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Derive the Axiom of Separation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A z $.  $d y ph z $.  $d A z $.  $( ` z ` is dummy. $)
    zfaus.1 $e |- A e. V $.
    $( Separation Scheme, which is an axiom scheme of Zermelo's original
       theory.  Scheme Sep of [BellMachover] p. 463.  In some textbooks this
       is given as a separate axiom; here we show it is redundant if we assume
       ~ ax-rep .  The Separation Scheme is a weak form of Frege's Axiom of
       Comprehension, conditioning it (with ` x e. A ` ) so that it asserts
       the existence of a collection only if it is smaller than some other
       collection ` A ` that already exists.  This prevents Russell's paradox
       ~ ru .  In some texts this scheme is called "Aussonderung" or the
       Subset Axiom.  In typical applications the variable ` x ` is free in the
       wff ` ph ` . $)
    zfaus   $p |- E. y A. x ( x e. y <-> ( x e. A /\ ph ) ) $=
      ( vz wel cv wcel weq wa wex wb wal wi a9e eqt eqcom syl6 adantrd
      19.21aiv 19.22i ax-mp a1i zfrep3cl an12 biex visset eleq1 birand ceqsexv
      bitr3 bilbi bial mpbi ) BCGZFHZDIZFBJZAKZKZFLZMZBNZCLUPBHZDIZAKZMZBNZCLUT
      FBCDEUTBCJZOZBNZCLZURCFJZCLVMCFPVNVLCVNVKBVNUSVJAVNUSCBJVJCFBQCBRSTUAUBUC
      UDUEVDVICVCVHBVBVGUPVBUSURAKZKZFLVGVPVAFUSURAUFUGVOVGFVEBUHUSURVFAUQVEDUI
      UJUKULUMUNUGUO $.
      $([5-Aug-93]$)
  $}

  ${
    $d x ph z $.  $d x y z $.  $( ` z ` is dummy. $)
    bm1.3ii.1 $e |- E. x A. y ( ph -> y e. x ) $.
    $( Convert implication to equivalence using Aussonderung.  Similar to
       Theorem 1.3ii of [BellMachover] p. 463. $)
    bm1.3ii   $p |- E. x A. y ( y e. x <-> ph ) $=
      ( vz wel wi wal wa wb wex weq a14b bilimd bialdv cbvexv mpbi cv visset
      zfaus pm3.2i exan 19.42v 19.26 bimsc1 19.20i sylbir 19.22i 19.23aiv
      ax-mp ) ACEFZGZCHZCBFZUKAIJZCHZBKZIZEKUNAJZCHZBKZUMUQEUMEKZUQAUNGZCHZBKVB
      DVDUMBEBELZVCULCVEUNUKABECMNOPQACBERESTUAUBURVAEURUMUPIZBKVAUMUPBUCVFUTBV
      FULUOIZCHUTULUOCUDVGUSCAUKUNUEUFUGUHUGUIUJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y z $.  $( ` z ` is dummy. $)
    $( No set contains all sets.  Theorem 41 of [Suppes] p. 30. $)
    nalset $p |- -. E. x A. y y e. x $=
      ( vz wel wn wex wal alexn wa wb cv visset zfaus weq a13b a14b bitrd
      binegd bi2and bi2bid a4b1 pclem6 syl 19.22i ax-mp mpgbi ) BADZEZBFZUGBGAF
      EAUGABHCBDZCADZCCDZEZIZJZCGZBFUIUMCBAKALMUPUHBUPBBDZUGUQEZIZJZUHUOUTCBCBN
      ZUJUQUNUSCBBOVAUKUGUMURCBAOVAULUQVAULBCDUQCBCOCBBPQRSTUAUQUGUBUCUDUEUF $.
      $([23-Aug-93]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The universal class is not a member of itself (and thus is not a set).
       Proposition 5.21 of [TakeutiZaring] p. 21; our proof, however, does
       not require the Axiom of Regularity. $)
    nvelv $p |- -. V e. V $=
      ( vx vy cvv wcel cv wceq wex wel wal nalset wb visset tbt bial dfcleq
      bitr4 biex mtbi isset mtbir ) CCDAEZCFZAGZBAHZBIZAGUCABJUEUBAUEUDBECDZKZB
      IUBUDUGBUFUDBLMNBUACOPQRACST $.
      $([23-Aug-93]$)
  $}

