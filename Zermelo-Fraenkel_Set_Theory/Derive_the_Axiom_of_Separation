$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Derive the Axiom of Separation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A z $.  $d y ph z $.  $d A z $.  $( ` z ` is dummy. $)
    zfaus.1 $e |- A e. V $.
    $( Separation Scheme, which is an axiom scheme of Zermelo's original
       theory.  Bell and Machover, p. 462.  Also called "Aussonderung."
       Typically, ` ph ` depends on ` x `. $)
    zfaus   $p |- E. y A. x ( x e. y <-> ( x e. A /\ ph ) ) $=
      ( vz wel cv wcel weq wa wex wb wal wi a9e eqt eqcom syl6 adantrd
      19.21aiv 19.22i ax-mp a1i zfrep3cl an12 biex visset eleq1 birand ceqsexv
      bitr3 bilbi bial mpbi ) BCGZFHZDIZFBJZAKZKZFLZMZBNZCLUPBHZDIZAKZMZBNZCLUT
      FBCDEUTBCJZOZBNZCLZURCFJZCLVMCFPVNVLCVNVKBVNUSVJAVNUSCBJVJCFBQCBRSTUAUBUC
      UDUEVDVICVCVHBVBVGUPVBUSURAKZKZFLVGVPVAFUSURAUFUGVOVGFVEBUHUSURVFAUQVEDUI
      UJUKULUMUNUGUO $.
      $([5-Aug-93]$)
  $}

  ${
    $d x ph z $.  $d x y z $.  $( ` z ` is dummy. $)
    bm1.3ii.1 $e |- E. x A. y ( ph -> y e. x ) $.
    $( Convert implication to equivalence using Aussonderung.  Similar to
       Theorem 1.3ii of Bell and Machover, p. 463. $)
    bm1.3ii   $p |- E. x A. y ( y e. x <-> ph ) $=
      ( vz wel wb wal wex wi wa weq a14b bilimd bialdv cbvexv mpbi cv visset
      zfaus pm3.2i exan 19.42v 19.26 bimsc1 19.20i sylbir 19.22i ax-mp a17ev )
      CBFZAGZCHZBIZEACEFZJZCHZUKUOAKGZCHZBIZKZEIUNEIUQUTEUQEIZUTAUKJZCHZBIVBDVD
      UQBEBELZVCUPCVEUKUOABECMNOPQACBERESTUAUBVAUNEVAUQUSKZBIUNUQUSBUCVFUMBVFUP
      URKZCHUMUPURCUDVGULCAUOUKUEUFUGUHUGUHUIUJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y z $.  $( ` z ` is dummy. $)
    $( No set contains all sets. $)
    nalset $p |- -. E. x A. y y e. x $=
      ( vz wel wn wex wal alexn wa wb cv visset zfaus weq a13b a14b bitrd
      binegd bi2and bi2bid a4b1 pclem6 syl 19.22i ax-mp mpgbi ) BADZEZBFZUGBGAF
      EAUGABHCBDZCADZCCDZEZIZJZCGZBFUIUMCBAKALMUPUHBUPBBDZUGUQEZIZJZUHUOUTCBCBN
      ZUJUQUNUSCBBOVAUKUGUMURCBAOVAULUQVAULBCDUQCBCOCBBPQRSTUAUQUGUBUCUDUEUF $.
      $([23-Aug-93]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The universal class is not a member of itself (and thus is not a set).
       Proposition 5.21, p. 21 of Takeuti and Zaring; our proof, however, does
       not require the Axiom of Regularity. $)
    nvelv $p |- -. V e. V $=
      ( vx vy cvv wcel cv wceq wex wel wal nalset wb visset tbt bial dfcleq
      bitr4 biex mtbi isset mtbir ) CCDAEZCFZAGZBAHZBIZAGUCABJUEUBAUEUDBECDZKZB
      IUBUDUGBUFUDBLMNBUACOPQRACST $.
      $([23-Aug-93]$)
  $}

