$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Extensionality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d z x w $.  $d z y w $.  $( ` w ` is dummy. $)
    $( A generalization of the Axiom of Extensionality in which ` x ` and
       ` y ` need not be distinct. $)
    zfext2 $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( vw weq wex wel wb wal wi a9e a14b bilbid 19.21aiv 19.15 syl eqt2b
      bi2imd ax-ext mpbii 19.23aiv ax-mp ) DBEZDFCAGZCBGZHZCIZABEZJZDBKUCUIDUCU
      DCDGZHZCIZADEZJUIUCULUGUMUHUCUKUFHZCIULUGHUCUNCUCUJUEUDDBCLMNUKUFCOPDBAQR
      CADSTUAUB $.
      $([15-Sep-93]$)
  $}

  ${
    $d x y z $.  $d ph z $.  $( ` z ` is dummy. $)
    bm1.1.1 $e |- ( ph -> A. x ph ) $.
    $( Any set which has a property is the only set with that property.
       Theorem 1.1 of Bell and Machover, p. 462. $)
    bm1.1   $p |- ( E. x A. y ( y e. x <-> ph ) ->
                  E! x A. y ( y e. x <-> ph ) ) $=
      ( vz wel wb wal wex wsb wa weq wi weu 19.26 biantr 19.20i ax-ext syl
      sylbir ax17el hbbi hbal a14b birbid bialdv ddel4 sylan5b gen2 jctr ax-17
      eu2 sylibr ) CBFZAGZCHZBIZUQUPUPBEJZKBELZMZEHBHZKUPBNUQVAUTBEUPCEFZAGZCHZ
      USURUPVDKUOVCKZCHZUSUOVCCOVFUNVBGZCHUSVEVGCUNAVBPQCBERSTUPVDBEVCBCVBABCEB
      UADUBUCUSUOVCCUSUNVBABECUDUEUFUGUHUIUJUPBEUPEUKULUM $.
      $([30-Jun-94]$)
  $}


