$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           The Axiom of Extensionality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( The Axiom of Extensionality ( ~ ax-ext ) restated so that it postulates
       the existence of a set ` z ` given two arbitrary sets ` x ` and ` y ` .
       This way of expressing it follows the general idea of the other ZFC
       axioms, which is to postulate the existence of sets given other
       sets.  $)
    axext $p |- E. z ( ( z e. x <-> z e. y ) -> x = y ) $=
      ( wel wb weq wi wex wal ax-ext 19.36v mpbir ) CADCBDEZABFZGCHMCINGABCJMNC
      KL $.
      $( [28-Sep-03] $)
  $}

  ${
    $d z x w $.  $d z y w $.  $( ` w ` is dummy. $)
    $( A generalization of the Axiom of Extensionality in which ` x ` and
       ` y ` need not be distinct. $)
    zfext2 $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( vw weq wex wel wb wal wi a9e ax-ext a14b birbid bialdv a8b bi2imd
      mpbii 19.23aiv ax-mp ) DAEZDFCAGZCBGZHZCIZABEZJZDAKUAUGDUACDGZUCHZCIZDBEZ
      JUGDBCLUAUJUEUKUFUAUIUDCUAUHUBUCDACMNODABPQRST $.
      $( [15-Sep-93] $)
  $}

  ${
    $d x y z $.  $d ph z $.  $( ` z ` is dummy. $)
    bm1.1.1 $e |- ( ph -> A. x ph ) $.
    $( Any set which has a property is the only set with that property.
       Theorem 1.1 of [BellMachover] p. 462. $)
    bm1.1 $p |- ( E. x A. y ( y e. x <-> ph ) ->
                  E! x A. y ( y e. x <-> ph ) ) $=
      ( vz wel wb wal wex wsb wa weq wi weu 19.26 biantr 19.20i ax-ext syl
      sylbir ax-17 hbbi hbal a14b birbid bialdv sbie sylan5b gen2 jctr eu2
      sylibr ) CBFZAGZCHZBIZUPUOUOBEJZKBELZMZEHBHZKUOBNUPUTUSBEUOCEFZAGZCHZURUQ
      UOVCKUNVBKZCHZURUNVBCOVEUMVAGZCHURVDVFCUMAVAPQBECRSTUOVCBEVBBCVAABVABUADU
      BUCURUNVBCURUMVAABECUDUEUFUGUHUIUJUOBEUOEUAUKUL $.
      $( [30-Jun-94] $)
  $}


