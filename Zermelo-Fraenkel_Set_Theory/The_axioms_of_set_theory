$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
   Postulate the Zermelo-Fraenkel axioms plus the Axiom of Choice $)

  $v u $.
  $( Let ` u ` be an individual variable. $)
  vu $f var u $.

$(
   The axioms are presented using defined connectives (` <-> `, ` E. `,
   etc.) for convenience.  However, it is implicitly understood that the
   actual axioms use only the primitive connectives ` -> `, ` -. `,
   ` A. `, ` = `, and ` e. `.  It is straightforward to establish the
   equivalence between the actual axioms and the ones we display, and we
   shall not do so.
$)

  ${
    $d x y z w $.

    $( Axiom of Extensionality.
       An axiom of Zermelo-Fraenkel set theory. $)
    ax-ext $a |- ( A. x ( x e. y <-> x e. z ) -> y = z ) $.

    $( Axiom of Replacement.
       An axiom scheme of Zermelo-Fraenkel set theory.  In this version, there
       are no restrictions on the variables that may occur in ` ph `. $)
    ax-rep $a |- E. x ( E. y A. z ( ph -> z = y ) ->
                        A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) $.

    $( Axiom of Union.
       An axiom of Zermelo-Fraenkel set theory. $)
    ax-un  $a |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $.

    $( Axiom of Power Sets.
       An axiom of Zermelo-Fraenkel set theory. $)
    ax-pow $a |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) $.

    $( Axiom of Regularity.
       An axiom of Zermelo-Fraenkel set theory. $)
    ax-reg $a |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $.

    $( Axiom of Infinity.
       An axiom of Zermelo-Fraenkel set theory. $)
    ax-inf $a |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $.

    $( Axiom of Choice. $)
    ax-ac $a  |- E. x A. y A. z ( ( y e. z /\ z e. w ) -> E. w A. y ( E. w
              ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $.
  $}




