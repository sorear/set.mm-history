$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Power class and union, intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.
    $( The power class of the intersection of two classes is the intersection
       of their power classes.  Exercise 4.12(j) of [Mendelson] p. 235. $)
    pwin $p |- P~ ( A i^i B ) = ( P~ A i^i P~ B ) $=
      ( vx cpw cin cv wss wa wcel ssin visset elpw bi2an 3bitr4 ineqri
      cleqcomi ) ADZBDZEABEZDZCQRTCFZAGZUABGZHUASGUAQIZUARIZHUATIUAABJUDUBUEUCU
      AACKZLUABUFLMUASUFLNOP $.
      $( [23-Nov-03] $) $( [23-Nov-03] $)

    $( The power class of the union of two classes includes the union of their
       power classes.  Exercise 4.12(k) of [Mendelson] p. 235. $)
    pwunss $p |- ( P~ A u. P~ B ) (_ P~ ( A u. B ) $=
      ( vx cpw cun cv wss wo wcel ssun elun visset elpw bi2or bitr 3imtr4
      ssriv ) CADZBDZEZABEZDZCFZAGZUCBGZHZUCUAGUCTIZUCUBIUCABJUGUCRIZUCSIZHUFUC
      RSKUHUDUIUEUCACLZMUCBUJMNOUCUAUJMPQ $.
      $( [23-Nov-03] $) $( [23-Nov-03] $)

    $( The power class of the union of two classes is a subset of the union of
       their power classes, iff one class is a subclass of the other.  Exercise
       4.12(l) of [Mendelson] p. 235. $)
    pwssun $p |- ( ( A (_ B \/ B (_ A ) <->
               P~ ( A u. B ) (_ ( P~ A u. P~ B ) ) $=
      ( vy vx wss wo cun cpw orcom wceq ssequn2 pweq eqimss syl sylbi ssequn1
      im2or ssun wn wa cv wcel wi cpr ssel csn unss12 visset snss syl2anb
      zfpair elpw df-pr sseq1i bitr2 sylib syl5 exp3a com23 imp31 elun prss
      bitr4 pm3.27bd pm3.26bd ord exp imp ssrdv exp31 bi2.15 syl6ib orrd impbi
      ) ABEZBAEZFZABGZHZAHZBHZGZEZVQVSVTEZVSWAEZFZWCVQVPVOFWFVOVPIVPWDVOWEVPVRA
      JZWDBAKWGVSVTJWDVRALVSVTMNOVOVRBJZWEABPWHVSWAJWEVRBLVSWAMNOQOVSVTWARNWCVO
      VPWCVOSZVPWCWITCBAWCWICUAZBUBZWJAUBZUCWCWKWIWLWCWKWLSZVOUCWIWLUCWCWKWMVOW
      CWKTZWMTDABWNWMDUAZAUBZWOBUBZUCWNWPWMWQWNWPWMWQUCWNWPTZWLWQWRWOWJUDZVTUBZ
      WSWAUBZFZWLWQFWRWSWBUBZXBWCWKWPXCWCWPWKXCWCWPWKXCWCWSVSUBZXCWPWKTZVSWBWSU
      EXEWOUFZWJUFZGZVREZXDXFAEXGBEXIWPWKXFAXGBUGWOADUHZUIWJBCUHZUIUJXDWSVREXIW
      SVRDCUKZULWSXHVRWOWJUMUNUOUPUQURUSUTWSVTWAVAUPWTWLXAWQWTWPWLWTWSAEWPWLTWS
      AXLULWOWJAXJXKVBVCVDXAWQWKXAWSBEWQWKTWSBXLULWOWJBXJXKVBVCVEQNVFVGUSVHVIVJ
      WLVOVKVLUSVHVIVGVMVN $.
      $( [23-Nov-03] $) $( [23-Nov-03] $)

    $( The power class of the union of two classes equals the union of their
       power classes, iff one class is a subclass of the other.  Part of
       Exercise 7(b) of [Enderton] p. 28. $)
    pwun $p |- ( ( A (_ B \/ B (_ A ) <->
               P~ ( A u. B ) = ( P~ A u. P~ B ) ) $=
      ( cun cpw wss wa wo wceq pwunss biantru pwssun eqss 3bitr4 ) ABCDZADBDCZE
      ZPONEZFABEBAEGNOHQPABIJABKNOLM $.
      $( [23-Nov-03] $) $( [23-Nov-03] $)
  $}

