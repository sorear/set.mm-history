$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subclasses and subsets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (_ $. $( Subclass or subset symbol $)
  $c (. $. $( Proper subclass or subset symbol $)

  wss $a wff A (_ B $.
  wpss $a wff A (. B $.

  ${
    $d x A $.  $d x B $.
    $( Define the subclass relationship between two classes.  Definition 5.9
       of Takeuti and Zaring, p. 17. $)
    df-ss $a |- ( A (_ B <-> A. x ( x e. A -> x e. B ) ) $.
  $}

  $( Define proper subclass relationsip between two classes.  Definition 5.9
     of Takeuti and Zaring, p. 17. $)
  df-pss $a |- ( A (. B <-> ( A (_ B /\ A =/= B ) ) $.

  ${
    $d y A $.  $d y B $.  $d x y $.
    dfssf.1 $e |- ( y e. A -> A. x y e. A ) $.
    dfssf.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Establish subclass relation requiring only that ` x ` not be
       free in ` A ` and ` B ` (but not necessarily absent from them). $)
    dfssf $p |- ( A (_ B <-> A. x ( x e. A -> x e. B ) ) $=
      ( wss cv wcel wi wal df-ss ax-17 hbim weq eleq1 bi2imd cbv4 bitr4 ) CDGBH
      ZCIZTDIZJZBKAHZCIZUDDIZJZAKBCDLUGUCABUGBMUAUBAEFNABOUEUAUFUBUDTCPUDTDPQRS
      $.
      $([3-Jul-94]$)

    $( If ` x ` is not free in ` A ` and ` B `, it is not free in
       ` A (_ B `. $)
    hbss   $p |- ( A (_ B -> A. x A (_ B ) $=
      ( wss cv wcel wi wal dfssf hba1 bial sylibr sylbi ) CDGZAHZCIRDIJZAKZQAKZ
      ABCDEFLZTTAKUASAMQTAUBNOP $.
      $([27-Dec-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Membership relationships follow from a subclass relationship. $)
    ssel $p |- ( A (_ B -> ( C e. A -> C e. B ) ) $=
      ( vx wss cv wceq wcel wa wex wi wal df-ss biimp 19.21bi imland 19.22dv
      df-clel 3imtr4g ) ABEZDFZCGZUAAHZIZDJUBUABHZIZDJCAHCBHTUDUFDTUCUEUBTUCUEK
      ZDTUGDLDABMNOPQDCARDCBRS $.
      $([5-Aug-93]$)
  $}

  ${
    ssi.1 $e |- A (_ B $.
    $( Membership inference from subclass relationship. $)
    ssi   $p |- ( C e. A -> C e. B ) $=
      ( wss wcel wi ssel ax-mp ) ABECAFCBFGDABCHI $.
      $([5-Aug-93]$)
    ${
      ssii.2 $e |- C e. A $.
      $( Membership inference from subclass relationship. $)
      ssii   $p |- C e. B $=
        ( wcel ssi ax-mp ) CAFCBFEABCDGH $.
        $([31-May-99]$)
    $}
  $}

  ${
    ssd.1 $e |- ( ph -> A (_ B ) $.
    $( Membership deduction from subclass relationship. $)
    ssd   $p |- ( ph -> ( C e. A -> C e. B ) ) $=
      ( wss wcel wi ssel syl ) ABCFDBGDCGHEBCDIJ $.
      $([15-Nov-95]$)
  $}

  ${
    $d x A $.  $d x B $.
    ssri.1 $e |- ( x e. A -> x e. B ) $.
    $( Inference rule based on subclass definition. $)
    ssri   $p |- A (_ B $=
      ( wss cv wcel wi df-ss mpgbir ) BCEAFZBGKCGHAABCIDJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    ssrdv.1 $e |- ( ph -> ( x e. A -> x e. B ) ) $.
    $( Deduction rule based on subclass definition. $)
    ssrdv   $p |- ( ph -> A (_ B ) $=
      ( cv wcel wi wal wss 19.21aiv df-ss sylibr ) ABFZCGNDGHZBICDJAOBEKBCDLM
      $.
      $([15-Nov-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Transitivity of subclasses.  Exercise 5 of Takeuti and Zaring, p. 17. $)
    sstr $p |- ( A (_ B -> ( B (_ C -> A (_ C ) ) $=
      ( vx wss cv wcel wi wal df-ss syl2 19.20ii 3imtr4g sylbi ) ABEDFZAGZOBGZH
      ZDIZBCEZACEZHDABJSQOCGZHZDIPUBHZDITUARUCUDDPQUBKLDBCJDACJMN $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Equality decomposes into two subclass relationships. $)
    eqss $p |- ( A = B <-> ( A (_ B /\ B (_ A ) ) $=
      ( vx cv wcel wb wal wi wa wceq wss albi dfcleq df-ss bi2an 3bitr4 ) CDZAE
      ZQBEZFCGRSHCGZSRHCGZIABJABKZBAKZIRSCLCABMUBTUCUACABNCBANOP $.
      $([5-Aug-93]$)
  $}

  ${
    eqssi.1 $e |- A (_ B $.
    eqssi.2 $e |- B (_ A $.
    $( Infer equality from two subclass relationships. $)
    eqssi   $p |- A = B $=
      ( wss wceq wa eqss biimpr mp2an ) ABEZBAEZABFZMKLGABHICDJ $.
      $([9-Sep-93]$)
  $}

  $( Any class is a subclass of itself.  Exercise 10 of Takeuti and Zaring,
     p. 18. $)
  ssid $p |- A (_ A $=
    ( wss wceq wa eqss cleqid mpbi pm3.26i ) AABZIAACIIDAAEAFGH $.
    $([5-Aug-93]$)

  $( Equality theorem for subclasses. $)
  sseq1 $p |- ( A = B -> ( A (_ C <-> B (_ C ) ) $=
    ( wss wb wa wi wceq sstr im2an eqss bi 3imtr4 cleqcoms ) ACDZBCDZEZBABADZAB
    DZFOPGZPOGZFBAHQRTSUABACIABCIJBAKOPLMN $.
    $([5-Aug-93]$)

  $( Equality theorem for the subclass relationship. $)
  sseq2 $p |- ( A = B -> ( C (_ A <-> C (_ B ) ) $=
    ( wss wa wi wceq wb sstr comm im2an eqss bi 3imtr4 ) ABDZBADZECADZCBDZFZRQF
    ZEABGQRHOSPTQORCABIJRPQCBAIJKABLQRMN $.
    $([25-Jun-98]$)

  $( Equality theorem for the subclass relationship. $)
  sseq12 $p |- ( ( A = B /\ C = D ) -> ( A (_ C <-> B (_ D ) ) $=
    ( wceq wss sseq1 sseq2 sylan9bb ) ABEACFBCFCDEBDFABCGCDBHI $.
    $([31-May-99]$)

  ${
    sseq1i.1 $e |- A = B $.
    $( An equality inference for the subclass relationship. $)
    sseq1i   $p |- ( A (_ C <-> B (_ C ) $=
      ( wceq wss wb sseq1 ax-mp ) ABEACFBCFGDABCHI $.
      $([18-Aug-93]$)

    $( An equality inference for the subclass relationship. $)
    sseq2i   $p |- ( C (_ A <-> C (_ B ) $=
      ( wceq wss wb sseq2 ax-mp ) ABECAFCBFGDABCHI $.
      $([30-Aug-93]$)

    ${
      sseq12i.2 $e |- C = D $.
      $( An equality inference for the subclass relationship. $)
      sseq12i   $p |- ( A (_ C <-> B (_ D ) $=
        ( wss sseq1i sseq2i bitr ) ACGBCGBDGABCEHCDBFIJ $.
        $([31-May-99]$)
    $}
  $}

  ${
    sseq1d.1 $e |- ( ph -> A = B ) $.
    $( An equality deduction for the subclass relationship. $)
    sseq1d   $p |- ( ph -> ( A (_ C <-> B (_ C ) ) $=
      ( wceq wss wb sseq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $([14-Aug-94]$)

    $( An equality deduction for the subclass relationship. $)
    sseq2d   $p |- ( ph -> ( C (_ A <-> C (_ B ) ) $=
      ( wceq wss wb sseq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $([14-Aug-94]$)

    ${
      sseq12d.2 $e |- ( ph -> C = D ) $.
      $( An equality deduction for the subclass relationship. $)
      sseq12d   $p |- ( ph -> ( A (_ C <-> B (_ D ) ) $=
        ( wss sseq1d sseq2d bitrd ) ABDHCDHCEHABCDFIADECGJK $.
        $([31-May-99]$)
    $}
  $}

  ${
    eqsstr.1 $e |- A = B $.
    eqsstr.2 $e |- B (_ C $.
    $( Substitution of equality into a subclass. $)
    eqsstr   $p |- A (_ C $=
      ( wss sseq1i mpbir ) ACFBCFABCDGEH $.
      $([16-Jul-95]$)
  $}

  ${
    sseqtr.1 $e |- A (_ B $.
    sseqtr.2 $e |- B = C $.
    $( Substitution of equality into a subclass. $)
    sseqtr   $p |- A (_ C $=
      ( wss sseq2i mpbi ) ABFACFBCAEGDH $.
      $([28-Jul-95]$)
  $}

  ${
    sseqtr4.1 $e |- A (_ B $.
    sseqtr4.2 $e |- C = B $.
    $( Substitution of equality into a subclass. $)
    sseqtr4   $p |- A (_ C $=
      ( wss sseq2i mpbir ) ACFABFCBAEGDH $.
      $([4-Apr-95]$)
  $}

  ${
    3sstr3.1 $e |- A (_ B $.
    3sstr3.2 $e |- A = C $.
    3sstr3.3 $e |- B = D $.
    $( Substitution of equality in both sides of a subclass. $)
    3sstr3   $p |- C (_ D $=
      ( wss sseq1i sseq2i bitr2 mpbir ) CDHZABHZNCBHMACBFIBDCGJKEL $.
      $([13-Jan-96]$)
  $}

  ${
    3sstr4.1 $e |- A (_ B $.
    3sstr4.2 $e |- C = A $.
    3sstr4.3 $e |- D = B $.
    $( Substitution of equality in both sides of a subclass. $)
    3sstr4   $p |- C (_ D $=
      ( cleqcomi 3sstr3 ) ABCDECAFHDBGHI $.
      $([13-Jan-96]$)
  $}

  ${
    3sstr4g.1 $e |- ( ph -> A (_ B ) $.
    3sstr4g.2 $e |- C = A $.
    3sstr4g.3 $e |- D = B $.
    $( Substitution of equality into both sides of a subclass. $)
    3sstr4g   $p |- ( ph -> C (_ D ) $=
      ( wss sseq12i sylibr ) ABCIDEIFDBECGHJK $.
      $([16-Aug-94]$)
  $}

  ${
    3sstr4d.1 $e |- ( ph -> A (_ B ) $.
    3sstr4d.2 $e |- ( ph -> C = A ) $.
    3sstr4d.3 $e |- ( ph -> D = B ) $.
    $( Substitution of equality into both sides of a subclass. $)
    3sstr4d   $p |- ( ph -> C (_ D ) $=
      ( wss sseq12d mpbird ) ADEIBCIADBECGHJFK $.
      $([30-Nov-95]$)
  $}

  $( Equality implies subclass. $)
  eqimss $p |- ( A = B -> A (_ B ) $=
    ( wceq wss sseq2 ssid mpbii ) ABCAADABDABAEAFG $.
    $([5-Aug-93]$)

  ${
    $d x A $.  $d x B $.
    $( Negation of subclass. $)
    nss $p |- ( -. A (_ B <-> E. x ( x e. A /\ -. x e. B ) ) $=
      ( cv wcel wi wn wex wal wa wss 19.14 annim biex df-ss bineg 3bitr4r ) ADZ
      BEZRCEZFZGZAHUAAIZGSTGJZAHBCKZGUAALUDUBASTMNUEUCABCOPQ $.
      $([25-Feb-96]$)
  $}

  ${
    $d ph y $.  $d ps y $.  $d x y $.  $( ` y ` is dummy. $)
    $( Equivalence of abstraction subclass and implication. $)
    ssab2 $p |- ( { x | ph } (_ { x | ps } <-> A. x ( ph -> ps ) ) $=
      ( vy cab wss cv wcel wi wal hbab1 dfssf abid bi2im bial bitr4 ) ACEZBCEZF
      CGZQHZSRHZIZCJABIZCJCDQRACDKBCDKLUCUBCATBUAACMBCMNOP $.
      $([3-Jul-94]$)
  $}

  ${
    $d ph y $.  $d ps y $.  $d x y $.  $( ` y ` is dummy. $)
    ssab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of abstraction subclass from implication. $)
    ssab2i $p |- { x | ph } (_ { x | ps } $=
      ( cab wss wi ssab2 mpgbir ) ACEBCEFABGCABCHDI $.
      $([31-Mar-95]$)
  $}

  $( Equivalence of abstraction subclass and implication. $)
  ssrab2 $p |- ( { x e. A | ph } (_ { x e. A | ps } <->
               A. x e. A ( ph -> ps ) ) $=
    ( crab wss cv wcel wa cab wi wral df-rab sseq12i wal ssab2 df-ral
    imdistanb bial bitr bicom ) ACDEZBCDEZFCGDHZAIZCJZUDBIZCJZFZABKZCDLZUBUFUCU
    HACDMBCDMNUIUEUGKZCOZUKUEUGCPUKUMUKUDUJKZCOUMUJCDQUNULCUDABRSTUATT $.
    $([30-May-99]$)

  ${
    $d x A $.
    $( Abstraction class restriction creates subclass. $)
    ssab $p |- { x | ( x e. A /\ ph ) } (_ A $=
      ( cv wcel wa cab pm3.26 ssab2i abid2 sseqtr4 ) BDCEZAFZBGLBGCMLBLAHIBCJK
      $.
      $([31-Mar-95]$)
  $}

  ${
    $d x A $.
    $( Abstraction class restriction creates subclass. $)
    ssrab $p |- { x e. A | ph } (_ A $=
      ( crab cv wcel wa cab df-rab ssab eqsstr ) ABCDBECFAGBHCABCIABCJK $.
      $([19-Mar-97]$)
  $}

  $( Alternate definition of proper subclass. $)
  dfpss2 $p |- ( A (. B <-> ( A (_ B /\ -. A = B ) ) $=
    ( wpss wss wne wa wceq wn df-pss df-ne bilan bitr ) ABCABDZABEZFMABGHZFABIN
    OMABJKL $.
    $([7-Feb-96]$)

  $( Alternate definition of proper subclass. $)
  dfpss3 $p |- ( A (. B <-> ( A (_ B /\ -. B (_ A ) ) $=
    ( wss wceq wn wa wpss eqss bineg bilan dfpss2 wi ibar pm5.74i iman 3bitr3
    bicon4 3bitr4 ) ABCZABDZEZFSSBACZFZEZFZABGSUBEFZUAUDSTUCABHIJABKUFUESUBLSUC
    LUFEUEESUBUCSUBMNSUBOSUCOPQR $.
    $([7-Feb-96]$)

  $( Equality theorem for proper subclass. $)
  psseq1 $p |- ( A = B -> ( A (. C <-> B (. C ) ) $=
    ( wceq wss wne wa wpss sseq1 neeq1 bi2and df-pss 3bitr4g ) ABDZACEZACFZGBCE
    ZBCFZGACHBCHNOQPRABCIABCJKACLBCLM $.
    $([7-Feb-96]$)

  $( Equality theorem for proper subclass. $)
  psseq2 $p |- ( A = B -> ( C (. A <-> C (. B ) ) $=
    ( wceq wss wne wa wpss sseq2 neeq2 bi2and df-pss 3bitr4g ) ABDZCAEZCAFZGCBE
    ZCBFZGCAHCBHNOQPRABCIABCJKCALCBLM $.
    $([7-Feb-96]$)

  $( A proper subclass is a subclass.  Theorem 10 of Suppes, p. 23. $)
  pssss $p |- ( A (. B -> A (_ B ) $=
    ( wpss wss wne wa df-pss biimp pm3.26d ) ABCZABDZABEZJKLFABGHI $.
    $([7-Feb-96]$)

  ${
    pssssd.1 $e |- ( ph -> A (. B ) $.
    $( Deduce subclass from proper subclass. $)
    pssssd   $p |- ( ph -> A (_ B ) $=
      ( wpss wss pssss syl ) ABCEBCFDBCGH $.
      $([29-Feb-96]$)
  $}

  $( Subclass in terms of proper subclass. $)
  sspss $p |- ( A (_ B <-> ( A (. B \/ A = B ) ) $=
    ( wss wpss wceq wo wn wa dfpss2 biimpr exp pm2.15d orrd pssss eqimss jaoi
    impbi ) ABCZABDZABEZFRSTRTSRTGZSSRUAHABIJKLMSRTABNABOPQ $.
    $([25-Feb-96]$)

  $( Proper subclass is irreflexive.  Theorem 7 of Suppes, p. 23. $)
  pssirr $p |- -. A (. A $=
    ( wpss wss wn wa dfpss3 pm3.24 mtbir ) AABAACZIDEAAFIGH $.
    $([7-Feb-96]$)

  $( Proper subclass has no 2-cycle loops.  Compare Theorem 8 of Suppes,
     p. 23. $)
  pssn2lp $p |- -. ( A (. B /\ B (. A ) $=
    ( wpss wa wss wn dfpss3 bi2an an41r3 bitr wo orc adantr ianor sylibr imlan
    sylbi pm3.24 mto ) ABCZBACZDZABEZBAEZDZUEFZDZUBUEUCFZUDFZDZDZUGUBUCUIDZUDUH
    DZDUKTULUAUMABGBAGHUCUIUDUHIJUJUFUEUJUHUIKZUFUHUNUIUHUILMUCUDNOPQUERS $.
    $([7-Feb-96]$)

  $( Partial trichotomy law for subclasses. $)
  ssnpss $p |- ( A (_ B -> -. B (. A ) $=
    ( wss wpss wceq wo wn sspss wi wa imnan pssn2lp mpbir psseq1 pssirr mtbii
    jaoi sylbi ) ABCABDZABEZFBADZGZABHSUBTSUBISUAJGSUAKABLMTAADUAABANAOPQR $.
    $([16-May-96]$)

  $( Transitive law for proper subclass.  Theorem 9 of Suppes, p. 23. $)
  psstr $p |- ( ( A (. B /\ B (. C ) -> A (. C ) $=
    ( wpss wa wss wceq wn sstr imp pssss syl2an psseq1 birand pssn2lp mtbiri
    con2 jca dfpss2 sylibr ) ABDZBCDZEZACFZACGZHZEACDUCUDUFABFZBCFZUDUAUBUGUHUD
    ABCIJABKBCKLUEUCUEUCCBDZUBEUEUAUIUBACBMNCBOPQRACST $.
    $([7-Feb-96]$)

  $( Transitive law for subclass and proper subclass. $)
  sspsstr $p |- ( ( A (_ B /\ B (. C ) -> A (. C ) $=
    ( wpss wceq wo wss wi psstr exp psseq1 biimprd jaoi imp sspss sylanb ) ABDZ
    ABEZFZBCDZACDZABGSTUAQTUAHRQTUAABCIJRUATABCKLMNABOP $.
    $([3-Apr-96]$)


  $( Transitive law for subclass and proper subclass. $)
  psssstr $p |- ( ( A (. B /\ B (_ C ) -> A (. C ) $=
    ( wpss wceq wo wss psstr exp psseq2 biimpcd jaod imp sspss sylan5b ) ABDZBC
    DZBCEZFZACDZBCGPSTPQTRPQTABCHIRPTBCAJKLMBCNO $.
    $([3-Apr-96]$)

  ${
    $d x A $.  $d x B $.
    $( Restriction of uniqueness to a smaller subclass. $)
    reuss $p |- ( ( A (_ B /\ ( E. x e. A ph /\ E! x e. B ph ) ) ->
                E! x e. A ph ) $=
    ( wss cv wcel wa wex weu wreu wrex wmo wi wal ssel imrand 19.21aiv euimmo
    syl eu5 biimpr exp syl9 imp32 df-reu sylibr df-rex bi2an sylan5b ) CDEZBFZC
    GZAHZBIZULDGZAHZBJZHZABCKZABCLZABDKZHUKUSHUNBJZUTUKUOURVCUKURUNBMZUOVCUKUNU
    QNZBOURVDNUKVEBUKUMUPACDULPQRUNUQBSTUOVDVCVCUOVDHUNBUAUBUCUDUEABCUFUGVAUOVB
    URABCUHABDUFUIUJ $.
    $([21-Aug-99]$)


    $( Restricted uniqueness "picks" a member of a subclass. $)
    reupick $p |- ( ( ( A (_ B /\ ( E. x e. A ph /\ E! x e. B ph ) ) /\ ph ) ->
                  ( x e. A <-> x e. B ) ) $=
    ( wss wrex wreu wa cv wcel wi ssel adantr wex weu ancrd imrand an1r syl6ib
    19.22dv eupick exp syl9 com23 imp32 df-rex df-reu bi2an sylan5b exp3a imp
    impbid ) CDEZABCFZABDGZHZHZAHBIZCJZURDJZUQUSUTKZAUMVAUPCDURLZMMUQAUTUSKUQUT
    AUSUQUTAUSUMUSAHZBNZUTAHZBOZHVEUSKZUPUMVDVFVGUMVFVDVGUMVDVEUSHZBNZVFVGUMVCV
    HBUMVCUTUSHZAHVHUMUSVJAUMUSUTVBPQUTUSARSTVFVIVGVEUSBUAUBUCUDUEUNVDUOVFABCUF
    ABDUGUHUIUJUDUKUL $.
    $([21-Aug-99]$)
  $}


