$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rank
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c R1 $. $( Cumulative hierarchy of sets function $)
  $c rank $. $( Rank function $)

  $( Extend class definition to include the cumulative hierarchy of sets
     function. $)
  cr1 $a class R1 $.

  $( Extend class definition to include rank function. $)
  crnk $a class rank $.

  ${
    $d x y z $.
    $( Define the cumulative hierarchy of sets function, using Takeuti and
       Zaring's notation ( ` R1 ` ).  Starting with the empty set, this
       function builds up layers of sets where the next layer is the power
       set of the previous layer (and the union of previous layers when the
       argument is a limit ordinal).  Using the Axiom of Regularity, we can
       show that any set whatsoever belongs to one of the layers of this
       hierarchy (see ~ tz9.13 ).  Our definition expresses Definition 9.9
       of [TakeutiZaring] p. 76 in a closed form, from which we derive the
       recursive definition as theorems ~ r10 , ~ r1suc , and ~ r1lim .
       Theorem ~ r1val1 shows a recursive definition that works for all values,
       and theorems ~ r1val2 and ~ r1val3 show the value expressed in terms of
       rank.  Other notations for this function are  _R_ with the argument
       as a subscript (Equation 3.1 of [BellMachover] p. 477), ` V ` with a
       a subscript (Definition of [Enderton] p. 202), _M_ with a subscript
       (Definition 15.19 of [Monk1] p. 113), and the capital Greek letter
       psi (Definition of [Mendelson] p. 281). $)
    df-r1 $a |- R1 = rec ( { <. x , y >. | y = P~ x } , (/) ) $.

    $( Define the rank function.  See ~ rankval , ~ rankval2 , or ~ rankval3
       its value.  The rank is a kind of "inverse" of the cumulative hierarchy
       of sets function ` R1 ` :  given a set, it returns an ordinal number
       telling us the smallest layer of the hierarchy to which the set belongs.
       Based on Definition 9.14 of [TakeutiZaring] p. 79.  Theorem ~ rankid
       illustrates the "inverse" concept.  $)
    df-rank $a |- rank =
          { <. x , y >. | y = |^| { z e. On | x e. ( R1 ` suc z ) } } $.
  $}

  ${
    $d x y z w v u $.  $d y z v A $.  $d y v u F $.
    trcl.1 $e |- A e. V $.
    trcl.2 $e |- F =
               ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) $.
    trcl.3 $e |- C = U. y e. om ( F ` y ) $.
    $( For any set ` A ` , show the properties of its transitive closure
       ` C ` . $)
    trcl $p |- ( A (_ C /\ Tr C /\ A. x ( ( A (_ x /\ Tr x ) -> C (_ x ) ) $=
      ( vv vu wss wtr cv wa wi wal com cfv ciun wrex c0 wcel peano1 wceq cuni
      cun copab crdg cres fveq1i cvv frzer ax-mp eqtr2 eqimss fveq2 sseq2d
      rcla4ev mp2an ssiun sseqtr4 wel dftr2 csuc eliun bilan r19.42v bitr4
      ssun2 fvex zfun2 unex ax-17 hbopab1 hbrdg hbres eleq2i bial 3imtr4 hbfv
      hbuni hbun unieq uneq12 mpian frsucopab mpan2 mpbiri ssd elunii syl5
      r19.22i sylbi peano2 eleq2d exp syl r19.23aiv weq cbvrexv sylibr ax-gen
      mpgbir wb treq mpbir wral sseq1d eqtr sseq1i biimpr adantr uniss sstr
      df-tr syl5ib anc2li unss syl6ibr biimprd syl9r com23 adantld finds2 comm
      r19.21aiv cbviunv iunss bitr 3pm3.2i ) EFMFNZEAOZMZYNNZPZFYNMZQZAREBSBOZG
      TZUAZFEUVAMZBSUBZEUVBMUCSUDEUCGTZMZUVDUEEUVEUFUVFUVEUCDOCOZUVGUGZUHZUFZCD
      UIZEUJZSUKZTZEUCGUVMIULZEUMUDUVNEUFHEUMUVKUNUOZUPEUVEUQUOUVCUVFBUCSYTUCUF
      UVAUVEEYTUCGURUSUTVABSUVAEVBUOJVCYMUVBNZUVQKLVDZLOZUVBUDZPZKOZUVBUDZQZLRK
      KLUVBVEUWDLUWAUWBYTVFZGTZUDZBSUBZUWCUWAUVRUVSUVAUDZPZBSUBZUWHUWAUVRUWIBSU
      BZPUWKUVTUWLUVRBUVSSUVAVGVHUVRUWIBSVIVJUWJUWGBSYTSUDZUWBUVAUGZUDUWGUWJUWM
      UWNUWFUWBUWMUWNUWFMUWNUVAUWNUHZMUWNUVAVKUWMUWFUWOUWNUWMUWOUMUDUWFUWOUFUVA
      UWNYTGVLZUVAUWPVMVNCDKEYTUVIUWOUMGUWBEUDCVOZKBVDCVOZCKUVAUWNCKYTGUWBUVMUD
      ZUWSCRUWBGUDZUWTCRCKUVLSCKEUVKUVJCDKVPUWQVQUWBSUDZCVOVRGUVMUWBIVSZUWTUWSC
      UXBVTWAUWRWBZCKUVAUXCWCWDIUVGUVAUFUVHUWNUFUVIUWOUFUVGUVAWEUVGUVAUVHUWNWFW
      GWHWIZUSWJWKUWBUVSUVAWLWMWNWOUWHUWBUVAUDZBSUBZUWCUWHUWBUVSGTZUDZLSUBZUXFU
      WGUXIBSUWMUWESUDZUWGUXIQYTWPUXJUWGUXIUXHUWGLUWESUVSUWEUFUXGUWFUWBUVSUWEGU
      RWQUTWRWSWTUXEUXHBLSBLXAUVAUXGUWBYTUVSGURWQXBXCBUWBSUVAVGXCWSXDXEFUVBUFYM
      UVQXFJFUVBXGUOXHYSAYQUWBGTZYNMZKSXIZYRYQUXLKSUXAYQUXLUXLUVEYNMZUVAYNMZUWF
      YNMZYQKBUWBUCUFUXKUVEYNUWBUCGURXJKBXAUXKUVAYNUWBYTGURXJUWBUWEUFUXKUWFYNUW
      BUWEGURXJYOUXNYPUXNYOUVEEYNUVEUVNEUVOUVPXKXLXMXNUWMYPUXOUXPQYOUWMUXOYPUXP
      UXOYPUWOYNMZUWMUXPUXOYPUXOUWNYNMZPUXQUXOYPUXRUXOUWNYNUGZMZYPUXRQUVAYNXOUX
      TUXSYNMUXRYPUWNUXSYNXPYNXQXRWSXSUVAUWNYNXTYAUWMUXPUXQUWMUWFUWOYNUXDXJYBYC
      YDYEYFYGYHYRKSUXKUAZYNMUXMFUYAYNFUVBUYAJBKSUVAUXKYTUWBGURYIXKXLKSUXKYNYJY
      KXCXDYL $.
      $([14-Sep-03]$)
  $}

  ${
    $d A x y z w v $.
    tz9.1.1 $e |- A e. V $.
    $( Every set has a smallest transitive extension.  Theorem 9.1 of
       [TakeutiZaring] p. 73. $)
    tz9.1 $p |- E. x ( A (_ x /\ Tr x
                /\ A. y ( ( A (_ y /\ Tr y ) -> x (_ y ) ) $=
      ( vz vv vw com cv cuni cun wceq copab crdg cres cfv ciun wss wtr wa wi
      wal w3a wex cleqid trcl omex fvex iunex sseq2 treq sseq1 bilimd bialdv
      bi3and cla4ev ax-mp ) CEHEIZFIGIZUSJKLGFMCNHOZPZQZRZVBSZCBIZRVESTZVBVERZU
      AZBUBZUCZCAIZRZVKSZVFVKVERZUAZBUBZUCZAUDBEGFCVBUTDUTUEVBUEUFVQVJAVBEHVAUG
      URUTUHUIVKVBLZVLVCVMVDVPVIVKVBCUJVKVBUKVRVOVHBVRVNVGVFVKVBVEULUMUNUOUPUQ
      $.
      $([15-Sep-03]$)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity, which does not require
       that ` A ` be a set.  Axiom 6' of [TakeutiZaring] p. 21.  The proof
       makes use of a special case of Proposition 9.4 of [TakeutiZaring]
       p. 75. $)
    zfregs $p |- ( -. A = (/) -> E. x ( x e. A /\ ( x i^i A ) = (/) ) ) $=
      ( vz vy vw c0 wceq wn cv wcel wex cin wa nnull csn wss wtr wi wal w3a
      snex tz9.1 wel trel inass incom ineq2i eqtr eleq2i elin bitr2 noel2
      sylbi exp syl6 exp3a com34 imp3a inss1 ssi ancri syl5 19.23adv syl5ib
      com23 imp a3d imland biran anass bitr 19.22dv visset zfauscl zfregw2
      snssi imlan snss ssin sylib syl comm 3adant3 19.23aiv ax-mp ) BFGHCIZBJZC
      KAIZBJZWHBLZFGZMZAKZCBNWGWMCWFOZDIZPZWOQZWNEIZPWRQMWOWRPRESZTZDKWGWMRZDEW
      NWFUAUBWTXADWPWQXAWSWPWQXAWQWPXAWQWPWGWMWQWOBLZFGHZWMWPWGMZWQWHXBJZXBWHLZ
      FGZMZAKWMXCWQXHWLAWQADUCZWIXGMZMZWLXHWQXIXJWLWQXIXJWLRWQXIMZXGWKWIXLWKXGW
      QXIWKHZXGHZRWQXMXIXNWQWRWJJZEKXIXNRZXMWQXOXPEWQEAUCZXOMXPXOWQXQXOXPWQXQXI
      XOXNWQXQXIXOXNRZWQXQXIMEDUCZXRWOWRWHUDXSXOXNXSXOMZWRXFJZXNYAWRWOWJLZJXTXF
      YBWRXFWOBWHLZLYBWOBWHUEYCWJWOBWHUFUGUHUIWRWOWJUJUKXFWRULUMUNUOUPUQURXOXQW
      JWHWRWHBUSUTVAVBVCEWJNVDVEVFVGVHUNURXHXIWIMZXGMXKXEYDXGWHWOBUJVIXIWIXGVJV
      KVDVLAXBWOBDVMVNVOVBXDWFXBJZXCXDWPWNBPZMZYEWGYFWPWFBVPVQYEWNXBPYGWFXBCVMV
      RWNWOBVSUKVTXBWFULWAVBUPWBVFWCWDWEWDUM $.
      $([17-Sep-03]$)
  $}

  ${
    $d x y z A $.
    $( Set (epsilon) induction.  Theorem 5.22 of [TakeutiZaring] p. 21. $)
    setind $p |- ( A. x ( x (_ A -> x e. A ) -> A = V ) $=
      ( vy cv wss wcel wi wal cvv cdif c0 wceq cin wa wex wn weq sseq1 eleq1
      bi2imd a4b1 ssindif0 syl5ibr eldifn nsyli imnan sylib nexdv zfregs nsyl2
      vdif0 sylibr ) ADZBEZUMBFZGZAHZIBJZKLZBILUQCDZURFZUTURMKLZNZCOUSUQVCCUQVA
      VBPGVCPUQVBUTBFZVAUQUTBEZVDVBUPVEVDGACACQUNVEUOVDUMUTBRUMUTBSTUAUTBUBUCUT
      IBUDUEVAVBUFUGUHCURUIUJBUKUL $.
      $([17-Sep-03]$)
  $}

  ${
    $d x A $.
    $( Set (epsilon) induction, stated compactly.  [Source unknown; given as
       a homework problem in 1992 by George Boolos (1940-1996).] $)
    setind2 $p |- ( P~ A (_ A -> A = V ) $=
      ( vx cpw wss cv wcel wi wal cvv wceq dfss2 visset elpw birim bial bitr
      setind sylbi ) ACZADZBEZADZUAAFZGZBHZAIJTUASFZUCGZBHUEBSAKUGUDBUFUBUCUAAB
      LMNOPBAQR $.
      $([17-Sep-03]$)
  $}

  ${
    $d x y $.
    $( The cumulative hierarchy of sets function is a function on the class of
       ordinal numbers. $)
    r1fnon $p |- R1 Fn On $=
      ( vy vx cr1 con0 wfn cv cpw wceq copab c0 crdg rdgfnon wb df-r1 fneq1
      ax-mp mpbir ) CDEZAFBFGHBAIZJKZDEZJSLCTHRUAMBANDCTOPQ $.
      $([5-Oct-03]$)

    $( Value of the cumulative hierarchy of sets function at ` (/) ` .
       Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r10 $p |- ( R1 ` (/) ) = (/) $=
      ( vy vx c0 cr1 cfv cv cpw wceq copab crdg df-r1 fveq1i zfnull rdgzer
      eqtr ) CDECAFBFGHBAIZCJZECCDQBAKLCPMNO $.
      $([2-Sep-03]$)
  $}

  ${
    $d x y z A $.
    $( Value of the cumulative hierarchy of sets function at a successor
       ordinal.  Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r1suc $p |- ( A e. On -> ( R1 ` suc A ) = P~ ( R1 ` A ) ) $=
      ( vx vy vz con0 wcel cr1 cfv cpw cvv csuc wceq fvex zfpowcl c0 cv ax-17
      df-r1 pweq rdgsucopab mpan2 ) AEFAGHZIZJFAKGHUCLUBAGMNBCDOABPZIUCJGDPZOFB
      QUEAFBQUEUCFBQBCRUDUBSTUA $.
      $([2-Sep-03]$)
  $}

  ${
    $d x y z A $.
    $( Value of the cumulative hierarchy of sets function at a limit
       ordinal.  Part of Definition 9.9 of [TakeutiZaring] p. 76. $)
    r1lim $p |- ( ( A e. B /\ Lim A ) -> ( R1 ` A ) = U. x e. A ( R1 ` x ) ) $=
      ( vz vy wcel wlim wa cv cpw wceq copab c0 crdg cfv ciun cr1 rdglim2a
      df-r1 fveq1i a1i iuneq2i 3eqtr4g ) BCFBGHBDIEIJKEDLZMNZOABAIZUEOZPBQOABUF
      QOZPAMBCUDRBQUEEDSZTABUHUGUHUGKUFBFUFQUEUITUAUBUC $.
      $([4-Oct-03]$)
  $}

  ${
    $d x y z A $.
    $( The cumulative hierarchy of sets is transitive.  Lemma 7T of [Enderton]
       p. 202. $)
    r1tr $p |- Tr ( R1 ` A ) $=
      ( vx vy vz con0 wcel cr1 cfv wtr cv c0 csuc wceq wb fveq2 treq syl weq
      tr0 r10 ax-mp mpbir wa wss wi wal cpw r1suc eleq2d visset elpw syl6bb
      adantr wel ssel dftr4 sylbi biimprd sylan9r ssrdv exp sylbid 19.21aiv
      dftr3 sylibr wlim wrex ciun cvv r1lim mpan eliun biimp syl6bi hba1 trss
      syl3 a4s r19.22d sylan9 sseq2d ssiun syl5bir syld tfinds wn cdm wfn
      r1fnon fndm eleq2i bineg ndmfv sylbir mpbiri pm2.61i ) AEFZAGHZIZBJZGHZIZ
      KGHZIZCJZGHZIZXELZGHZIZWSBCAWTKMXAXCMXBXDNWTKGOXAXCPQBCRXAXFMXBXGNWTXEGOX
      AXFPQWTXHMXAXIMXBXJNWTXHGOXAXIPQWTAMXAWRMXBWSNWTAGOXAWRPQXDKIZSXCKMXDXKNT
      XCKPUAUBXEEFZXGXJXLXGUCZWTXIFZWTXIUDZUEZBUFXJXMXPBXMXNWTXFUDZXOXLXNXQNXGX
      LXNWTXFUGZFXQXLXIXRWTXEUHZUIWTXFBUJZUKULUMXMXQXOXMXQUCDWTXIXQDBUNDJZXFFZX
      MYAXIFZWTXFYAUOXGYBYAXRFZXLYCXGXFXRUDYBYDUEXFUPXFXRYAUOUQXLYCYDXLXIXRYAXS
      UIURUSUSUTVAVBVCBXIVDVEVAWTVFZCBUNZXGUEZCUFZXBYEYHUCZYAXAFZYAXAUDZUEZDUFX
      BYIYLDYIYJYAXFUDZCWTVGZYKYEYJYBCWTVGZYHYNYEYJYACWTXFVHZFZYOYEXAYPYAWTVIFY
      EXAYPMXTCWTVIVJVKZUIYQYOCYAWTXFVLVMVNYHYBYMCWTYGCVOYGYFYBYMUEZUECXGYSYFXF
      YAVPVQVRVSVTYEYNYKUEYHYEYKYAYPUDYNYEXAYPYAYRWACWTXFYAWBWCUMWDVCDXAVDVEVAW
      EWQWFZWSXKSYTWRKMZWSXKNYTAGWGZFZWFUVAUVCWQUVBEAGEWHUVBEMWIEGWJUAWKWLAGWMW
      NWRKPQWOWP $.
      $([26-Nov-03]$)
  $}

  ${
    $d x y A $. $d x y B $.
    $( Ordering relation for the cumulative hierarchy of sets.  Part of
       Proposition 9.10(2) of [TakeutiZaring] p. 77. $)
    r1ord $p |- ( B e. On -> ( A e. B -> ( R1 ` A ) e. ( R1 ` B ) ) ) $=
      ( vx vy con0 wcel cr1 cfv wi csuc wa wss cv wceq eleq2 fveq2 eleq2d
      bi2imd weq onelon cpw fvex pwid r1suc mpbiri syl exp wtr r1tr trss ax-mp
      ssd syl3d cvv elisset sucexbg sylibr sucssel imp syl7 a1d com24 exp3a
      imp31 wlim wel wal ciun wrex rcla4ev limsuc biimpa word limord visset
      elon sylan sylan13 eliun wb r1lim mpan adantr mpbird ad2antll tfindsg
      pm3.26 suceloni jca eloni ordsucss pm2.43d ) BEFZABFZAGHZBGHZFZWMWNWNWQIZ
      WMAJZEFZKWSBLZWRWMWNKZACMZFZWOXCGHZFZIZAWSFZWOWSGHZFZIADMZFZWOXKGHZFZIZAX
      KJZFZWOXPGHZFZIZWRCDBWSXCWSNZXDXHXFXJXCWSAOYAXEXIWOXCWSGPQRCDSZXDXLXFXNXC
      XKAOYBXEXMWOXCXKGPQRXCXPNZXDXQXFXSXCXPAOYCXEXRWOXCXPGPQRXCBNZXDWNXFWQXCBA
      OYDXEWPWOXCBGPQRWTXHXJWTXHKAEFZXJWSATYEXJWOWOUAZFWOAGUBUCYEXIYFWOAUDQUEZU
      FUGXKEFZWTWSXKLZXOXTIZYHWTYIYJYHXQXOWTYIKZXSYHXOYKXSIIXQYHXOXLXSYKYHXNXSX
      LYHXMXRWOYHXMXRFZXMXRLZYHYLXMXMUAZFXMXKGUBUCYHXRYNXMXKUDQUEXRUHYLYMIXPUIX
      RXMUJUKUFULUMWTYIXLWTAUNFZYIXLIWTWSUNFYOWSEUOAUPUQAXKUNURUFUSUTVAVBVCVDXC
      VEZWTKWSXCLZKXGDCVFYIKXOIDVGYPXGWTYQYPXDXFYPXDKZXFWODXCXMVHZFZYRXNDXCVIZY
      TWSXCFZXJUVAYRXNXJDWSXCXKWSNXMXIWOXKWSGPQVJYPXDUVBXCAVKVLYRYEXJXCEFZXDYEY
      PXCATYPXCVMUVCXCVNXCCVOZVPUQVQYGUFVRDWOXCXMVSUQYPXFYTVTXDYPXEYSWOXCUNFYPX
      EYSNUVDDXCUNWAWBQWCWDUGWEVAWFXBWMWTWMWNWGXBYEWTBATAWHUFWIWMWNXAWMBVMWNXAI
      BWJABWKUFUSVRUGWL $.
      $([8-Sep-03]$)
  $}

  $( Ordering relation for the cumulative hierarchy of sets.  Part of
     Proposition 9.10(2) of [TakeutiZaring] p. 77. $)
  r1ord2 $p |- ( B e. On -> ( A e. B -> ( R1 ` A ) (_ ( R1 ` B ) ) ) $=
    ( con0 wcel cr1 cfv wss r1ord wtr wi r1tr trss ax-mp syl6 ) BCDABDAEFZBEFZD
    ZOPGZABHPIQRJBKPOLMN $.
    $([22-Sep-03]$)

  $( Ordering relation for the cumulative hierarchy of sets.  Part of
     Theorem 3.3(i) of [BellMachover] p. 478. $)
  r1ord3 $p |- ( ( A e. On /\ B e. On ) -> ( A (_ B ->
               ( R1 ` A ) (_ ( R1 ` B ) ) ) $=
    ( con0 wcel wa wss wceq wo cr1 cfv onsseleq wi r1ord2 adantl fveq2 eqimss
    syl a1i jaod sylbid ) ACDZBCDZEZABFABDZABGZHAIJZBIJZFZABKUCUDUHUEUBUDUHLUAA
    BMNUEUHLUCUEUFUGGUHABIOUFUGPQRST $.
    $([22-Sep-03]$)

  ${
    $d x y z w v A $.
    $( The value of the cumulative hierarchy of sets function expressed
       recursively.  Theorem 7Q of [Enderton] p. 202. $)
    r1val1 $p |- ( A e. On -> ( R1 ` A ) = U. x e. A P~ ( R1 ` x ) ) $=
      ( vy con0 wcel cr1 cfv cv cpw ciun wss wa wceq c0 csuc wrex cvv wlim w3o
      onzsl nullss fveq2 r10 syl6eq sseq1d mpbiri ax-17 hbiu1 hbss visset
      sucid eleq2 ssiun2 syl adantl r1suc sylan9eqr mpbird exp r19.23ai wral
      word ordelon limord sylan sucelon r1ord2 mpi sylbi sseq2d mpbid
      r19.21aiv ss2iun r1lim 3jaoi r1ord3 onelon sylib pm3.26 jca wi eloni
      ordsucss imp syl13 iunss sylibr eqss ) BDEZBFGZABAHZFGZIZJZKZWNWJKZLWJWNM
      WIWOWPWIBNMZBWKOZMZADPZBQEZBRZLZSWOABTWQWOWTXCWQWONWNKWNUAWQWJNWNWQWJNFGN
      BNFUBUCUDUEUFWSWOADACWJWNCHWJEAUGACBWMUHUIWKDEZWSWOXDWSLZWOWMWNKZWSXFXDWS
      WKBEZXFWSXGWKWREZWKAUJUKZBWRWKULUFABWMUMUNUOXEWJWMWNWSXDWJWRFGZWMBWRFUBWK
      UPZUQUEURUSUTXCWOABWLJZWNKZXBXMXAXBWLWMKZABVAXMXBXNABXBXGXNXBXGLXDXNBVBZX
      GXDXBBWKVCBVDVEXDWLXJKZXNXDWRDEZXPWKVFZXQXHXPXIWKWRVGVHVIXDXJWMWLXKVJVKUN
      USVLABWLWMVMUNUOXCWJXLWNABQVNUEURVOVIWIWMWJKZABVAWPWIXSABWIXGXSWIXGLZXJWJ
      KZXSXQWILWRBKZYAXTWRBVPXTXQWIXTXDXQBWKVQZXRVRWIXGVSVTWIXGYBWIXOXGYBWABWBW
      KBWCUNWDWEXTXJWMWJXTXDXJWMMYCXKUNUEVKUSVLABWMWJWFWGVTWJWNWHWG $.
      $([?]$)
  $}

  ${
    $d x y z w v A $.  $d x y u F $.
    tz9.12lem.1 $e |- A e. V $.
    tz9.12lem.2 $e |- F =
                   { <. z , w >. | w = |^| { v e. On | z e. ( R1 ` v ) } } $.
    $( Lemma for ~ tz9.12 . $)
    tz9.12lem1 $p |- ( F " A ) (_ On $=
      ( vy vx cima con0 cv wcel cop wa wex visset elima cr1 cfv crab cint wceq
      copab eleq2i weq eleq1 birabsdv inteqd cleq2d cleq1 opelopab bitr cvv
      19.8a isset sylibr c0 wn wi intex wss ssrab oninton mpan syl5bir comm
      sylbir mpcom sylbi adantl 19.23aiv ssriv ) HEDJZKHLZVNMILZDMZVPVONZEMZOZI
      PVOKMZIVOEDHQZRVTWAIVSWAVQVSVOVPCLSTZMZCKUAZUBZUCZWAVSVRBLZALZWCMZCKUAZUB
      ZUCZABUDZMWGEWNVRGUEWMWHWFUCWGABVPVOIQWBAIUFZWLWFWHWOWKWEWOWJWDCKWIVPWCUG
      UHUIUJWHVOWFUKULUMWFUNMZWGWAWGWGHPWPWGHUOHWFUPUQWPWEURUCUSZWGWAUTWEVAWGWQ
      WAWGWAWFKMZWQVOWFKUGWEKVBWQWRWDCKVCWEVDVEVFVGVHVIVJVKVLVJVM $.
      $([22-Sep-03]$)

    $( Lemma for ~ tz9.12 . $)
    tz9.12lem2 $p |- suc U. ( F " A ) e. On $=
      ( cima cuni con0 wss wcel tz9.12lem1 wfun cvv cv cr1 cfv crab cint wceq
      copab funopabeq wb funeq ax-mp mpbir zfrep5 onuni onsuc ) EDHZIZUKJKULJLA
      BCDEFGMUKENZUKOLUMBPAPCPQRLCJSTZUAABUBZNZABUNUCEUOUAUMUPUDGEUOUEUFUGEDFUH
      UFUIUFUJ $.
      $([22-Sep-03]$)

    $( Lemma for ~ tz9.12 . $)
    tz9.12lem3 $p |- ( A. x e. A E. y e. On x e. ( R1 ` y ) ->
                    A e. ( R1 ` suc suc U. ( F " A ) ) ) $=
      ( cv cr1 cfv wcel con0 wrex wral cima cuni csuc wa crab c0 wceq wn weq
      fveq2 eleq2d rcla4ev rnoel3 sylibr cint cvv intex copab visset eleq1
      birabsdv inteqd fvopabg mpan fveq1i syl5eq sylbi wss ssrab onint eqeltrd
      wal wel ax-17 hbrab1 hbint hbeq hbopab eleq2i bial 3imtr4 hbfv hbel
      elrabf pm3.27bd 3syl adantr cdm wi cop wex cleq2d cleq1 opelopab bitr
      biex eldm isset 3bitr4 bitr4 wfun funopabeq wb funeq ax-mp mpbir
      funfvima syl tz9.12lem1 onsucuni ssi tz9.12lem2 r1ord2 syl6 imp ssd mpd
      exp31 com3r r19.23adv r19.20i cpw r1suc elpw dfss3 3bitr ) AJZBJZKLZMZBNO
      ZAFPYCGFQZRSZKLZMZAFPZFYISKLZMZYGYKAFYCFMZYFYKBNYDNMZYFYOYKYPYFYOYKYPYFTZ
      YOTZYCYCGLZKLZMZYKYQUVAYOYQYCEJZKLZMZENUAZUBUCUDZYSUVEMZUVAYQUVDENOUVFUVD
      YFEYDNEBUEUVCYEYCUVBYDKUFUGUHUVDENUIUJZUVFYSUVEUKZUVEUVFUVIULMZYSUVIUCUVE
      UMZUVJYCDJZCJZUVCMZENUAZUKZUCZCDUNZLZUVIYSYCULMUVJUVSUVIUCAUOZCDYCUVPUVIU
      LULCAUEZUVOUVEUWAUVNUVDENUVMYCUVCUPUQURZUSUTYCGUVRIVAVBVCUVENVDUVFUVIUVEM
      UVDENVEUVEVFUTVGUVGYSNMUVAUVDUVAEBYSNEBYCGYDUVRMZUWCEVHYDGMZUWDEVHUVQCDEB
      EBBUVLUVPBDVIEVJEBUVOUVNEBNVKVLVMVNGUVRYDIVOZUWDUWCEUWEVPVQBAVIEVJZVRZYPE
      VJEBBYCYTUWFEBYSKYDKMEVJUWGVRVSUVBYSUCUVCYTYCUVBYSKUFUGVTWAWBWCYRYTYJYCYQ
      YOYTYJVDZYQYOYSYHMZUWHYQYCGWDMZYOUWIWEZYQUVFUWJUVHUWJUVJUVFYCYDWFZGMZBWGY
      DUVIUCZBWGUWJUVJUWMUWNBUWMUWLUVRMUWNGUVRUWLIVOUVQUVLUVIUCUWNCDYCYDUVTBUOU
      WAUVPUVIUVLUWBWHUVLYDUVIWIWJWKWLBYCGUVTWMBUVIWNWOUVKWPUJGWQZUWJUWKUWOUVRW
      QZCDUVPWRGUVRUCUWOUWPWSIGUVRWTXAXBFYCGXCUTXDUWIYSYIMZUWHYHYIYSYHNVDYHYIVD
      CDEFGHIXEYHXFXAXGYINMZUWQUWHWECDEFGHIXHZYSYIXIXAXDXJXKXLXMXNXOXPXQYNFYJXR
      ZMFYJVDYLYMUWTFUWRYMUWTUCUWSYIXSXAVOFYJHXTAFYJYAYBUJ $.
      $([22-Sep-03]$)
  $}

  ${
    $d x y z w v A $.
    tz9.12.1 $e |- A e. V $.
    $( A set is well-founded if all of its elements are well-founded.
       Proposition 9.12 of [TakeutiZaring] p. 78.  The main proof consists
       of ~ tz9.12lem1 through ~ tz9.12lem3 . $)
    tz9.12 $p |- ( A. x e. A E. y e. On x e. ( R1 ` y ) ->
              E. y e. On A e. ( R1 ` y ) ) $=
      ( vw vz vv cv cr1 cfv wcel con0 wrex wral crab cint wceq copab cima cuni
      csuc cleqid tz9.12lem3 tz9.12lem2 onsuc fveq2 eleq2d rcla4ev mpan syl )
      AHBHZIJZKBLMACNCEHFHGHIJKGLOPQFERZCSTUAZUAZIJZKZCULKZBLMZABFEGCUMDUMUBZUC
      UOLKUQUSUNFEGCUMDUTUDUEURUQBUOLUKUOQULUPCUKUOIUFUGUHUIUJ $.
      $([22-Sep-03]$)
  $}

  ${
    $d x y z w A $.
    tz9.13.1 $e |- A e. V $.
    $( Every set is well-founded, assuming the Axiom of Regularity.  In other
       words, every set belongs to a layer of the cumulative hierarchy of sets.
       Proposition 9.13 of [TakeutiZaring] p. 78. $)
    tz9.13 $p |- E. x e. On A e. ( R1 ` x ) $=
      ( vy vz vw cv cr1 cfv wcel con0 wrex cab cvv wss wi wceq setind wral wel
      ssel visset weq eleq1 birexdv elab syl6ib r19.21aiv tz9.12 syl sylibr
      mpg eleqtrr mpbi ) BDGZAGHIZJZAKLZDMZJBUPJZAKLZBNUSCEGZUSOZVBUSJZPUSNQEEU
      SRVCVBUPJZAKLZVDVCFGZUPJZAKLZFVBSVFVCVIFVBVCFETVGUSJVIVBUSVGUAURVIDVGFUBD
      FUCUQVHAKUOVGUPUDUEUFUGUHFAVBEUBZUIUJURVFDVBVJDEUCUQVEAKUOVBUPUDUEUFUKULU
      MURVADBCUOBQUQUTAKUOBUPUDUEUFUN $.
      $([23-Sep-03]$)
  $}

  ${
    $d x y A $.
    $( Every set is well-founded, assuming the Axiom of Regularity.
       Proposition 9.13 of [TakeutiZaring] p. 78.  This variant of ~ tz9.13
       expresses the class existence requirement as an antecedent. $)
    tz9.13g $p |- ( A e. B -> E. x e. On A e. ( R1 ` x ) ) $=
      ( vy cv cr1 cfv wcel con0 wrex wceq ax-17 eleq1 birexd visset tz9.13
      vtoclg ) DEZAEFGZHZAIJBSHZAIJDBCRBKZTUAAIUBALRBSMNARDOPQ $.
      $([4-Oct-03]$)
  $}

  ${
    $d x A $.
    $( Every set is well-founded, assuming the Axiom of Regularity.
       Proposition 9.13 of [TakeutiZaring] p. 78.   This variant of
       ~ tz9.13g is useful in proofs of theorems about the rank function. $)
    rankwflem $p |- ( A e. B -> E. x e. On A e. ( R1 ` suc x ) ) $=
      ( wcel cv cr1 cfv con0 wrex csuc tz9.13g wss suceloni visset sucid
      r1ord2 mpi syl ssd r19.22i ) BCDBAEZFGZDZAHIBUAJZFGZDZAHIABCKUCUFAHUAHDZU
      BUEBUGUDHDZUBUELZUAMUHUAUDDUIUAANOUAUDPQRSTR $.
      $([4-Oct-03]$)
  $}

  ${
    $d x y $.
    $( Every set belongs to some value of the cumulative hierarachy of sets
       function ` R1 ` , i.e. the indexed union of all values of ` R1 ` is the
       universe.  Lemma 9.3 of [Jech] p. 71. $)
    jech9.3 $p |- U. x e. On ( R1 ` x ) = V $=
      ( vy con0 cv cr1 cfv ciun cvv wcel wrex visset tz9.13 eliun mpbir 2th
      cleqri ) BACADEFZGZHBDZRIZSHITSQIACJASBKZLASCQMNUAOP $.
      $([12-Oct-03]$)
  $}

  ${
    $d x y z A $.
    rankval.1 $e |- A e. V $.
    $( Value of the rank function.  Definition 9.14 of [TakeutiZaring]
       p. 79 (proved as a theorem from our definition). $)
    rankval $p |- ( rank ` A ) = |^| { x e. On | A e. ( R1 ` suc x ) } $=
      ( vz vy crnk cfv cv csuc cr1 wcel con0 crab cint wceq copab df-rank
      fveq1i c0 wn cvv wrex rankwflem ax-mp rnoel3 mpbir intex mpbi eleq1
      birabsdv inteqd fvopab eqtr ) BFGBDHEHZAHIJGZKZALMZNZOEDPZGBUOKZALMZNZBFU
      SEDAQREDBURVBCVASOTZVBUAKVCUTALUBZBUAKVDCABUAUCUDUTALUEUFVAUGUHUNBOZUQVAV
      EUPUTALUNBUOUIUJUKULUM $.
      $([24-Sep-03]$)
  $}

  ${
    $d x y A $.
    $( Value of the rank function.  Definition 9.14 of [TakeutiZaring]
       p. 79 (proved as a theorem from our definition).  This more general
       variant of ~ rankval expresses the class existence requirement as an
       antecedent instead of a hypothesis. $)
    rankvalg $p |- ( A e. B ->
                   ( rank ` A ) = |^| { x e. On | A e. ( R1 ` suc x ) } ) $=
      ( vy cv crnk cfv csuc cr1 wcel con0 crab cint wceq fveq2 eleq1 birabsdv
      inteqd cleq12d visset rankval vtoclg ) DEZFGZUCAEHIGZJZAKLZMZNBFGZBUEJZAK
      LZMZNDBCUCBNZUDUIUHULUCBFOUMUGUKUMUFUJAKUCBUEPQRSAUCDTUAUB $.
      $([5-Oct-03]$)

    $( Value of an alternate definition of the rank function.  Definition of
       [BellMachover] p. 478. $)
    rankval2 $p |- ( A e. B ->
                   ( rank ` A ) = |^| { x e. On | A (_ ( R1 ` x ) } ) $=
      ( wcel crnk cfv cv csuc cr1 con0 crab cint wss rankvalg cpw r1suc eleq2d
      cvv wb fvex elpw2g ax-mp syl6bb birabi inteqi syl6eq ) BCDBEFBAGZHIFZDZAJ
      KZLBUGIFZMZAJKZLABCNUJUMUIULAJUGJDZUIBUKOZDZULUNUHUOBUGPQUKRDUPULSUGITBUK
      RUAUBUCUDUEUF $.
      $([8-Oct-03]$)
  $}

  ${
    $d x A $.
    $( The rank of a set is an ordinal number.  Proposition 9.15(1) of
       [TakeutiZaring] p. 79. $)
    rankon $p |- ( rank ` A ) e. On $=
      ( vx cvv wcel crnk cfv con0 cv csuc cr1 crab cint rankvalg c0 wceq wn
      wrex rankwflem rnoel3 sylibr wss ssrab oninton mpan syl eqeltrd 0elon
      fvprc eleq1d mpbiri pm2.61i ) ACDZAEFZGDZULUMABHIJFDZBGKZLZGBACMULUPNOPZU
      QGDZULUOBGQURBACRUOBGSTUPGUAURUSUOBGUBUPUCUDUEUFULPZUNNGDUGUTUMNGAEUHUIUJ
      UK $.
      $([5-Oct-03]$)
  $}

  ${
    $d x y A $.
    rankid.1 $e |- A e. V $.
    $( Identity law for the rank function. $)
    rankid $p |- A e. ( R1 ` suc ( rank ` A ) ) $=
      ( vx vy cv csuc cr1 cfv wcel con0 crab cint crnk wrex cvv rankwflem
      ax-mp ax-17 hbrab1 hbint hbsuc hbfv hbel wceq suceq fveq2d eleq2d
      onminsb rankval fveq2i eleqtrr ) AACEZFZGHZIZCJKZLZFZGHZAMHZFZGHUOCJNZAUS
      IZAOIVBBCAOPQUOVCCCDDAUSDEZAICRCDURGVDGICRCDUQCDUPUOCDJSTUAUBUCULUQUDZUNU
      SAVEUMURGULUQUEUFUGUHQVAURGUTUQUDVAURUDCABUIUTUQUEQUJUK $.
      $([4-Oct-03]$)
  $}

  ${
    rankr1lem.1 $e |- A e. V $.
    $( Lemma for ~ rankr1 . $)
    rankr1lem $p |- ( B e. On ->
                    ( -. A e. ( R1 ` B ) -> B (_ ( rank ` A ) ) ) $=
      ( con0 wcel cr1 cfv wn crnk csuc wss wb rankon onsuc ontri1 mpan rankid
      wi r1ord3 ssel syl6 mpii sylbird pm2.15d onsssuc mpan2 sylibrd ) BDEZABFG
      ZEZHBAIGZJZEZBUKKZUHUMUJUHUMHZULBKZUJULDEZUHUPUOLUKAMZNZULBOPUHUPAULFGZEZ
      UJACQUHUPUTUIKZVAUJRUQUHUPVBRUSULBSPUTUIATUAUBUCUDUHUKDEUNUMLURBUKUEUFUG
      $.
      $([6-Oct-03]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    rankr1.1 $e |- A e. V $.
    $( A relationship between the rank function and the cumulative hierarchy
       of sets function ` R1 ` .  Proposition 9.15(2) of [TakeutiZaring]
       p. 79. $)
    rankr1 $p |- ( B = ( rank ` A ) <->
                 ( -. A e. ( R1 ` B ) /\ A e. ( R1 ` suc B ) ) ) $=
      ( vx vy crnk cfv wceq cr1 wcel wn csuc wa con0 rankon eleq1 mpbiri word
      wi eloni c0 cv wrex wlim w3o ordzsl noel fveq2 r10 syl6eq eleq2d mtbiri
      a1d wss visset sucid eleq2 adantl wb ontri1 bicon2d eleq1a imp suceloni
      sylan pm3.26 sylan13 mpbid adantr crab cint rankval cleq2i biimp sseq1d
      weq suceq fveq2d onintss syl5bir biimpa sylan5d exp4d com3l imp31 mtod
      exp31 r19.23aiv onel syl6bi anc2li r1ord2 mpi syl ssd sseq1i syl6ibr
      syld sseq1 biimprd sylan9r sylibd syl6 pm2.03 pm2.43a con2 ciun cvv
      r1lim fvex eliun syl6bb mtbird exp comm 3jaoi sylbi mpcom ssrab
      rankwflem ax-mp rnoel3 mpbir onint mp2an eqeltr elrab pm3.27bd jca
      rankr1lem cdm ndmfv a3i wfn r1fnon fndm eleq2i sucelon bitr4 sylib syl5
      sylibr eqss impbi ) BAFGZHZABIGZJZKZABLZIGZJZMZUVFUVIUVLBNJZUVFUVIUVFUVNU
      VENJAOZBUVENPQZUVNBRZUVFUVISZBTUVQBUAHZBDUBZLZHZDNUCZBUDZUEUVRDBUFUVSUVRU
      WCUWDUVSUVIUVFUVSUVHAUAJZAUGZUVSUVGUAAUVSUVGUAIGUABUAIUHUIUJUKULUMUWBUVRD
      NUVTNJZUWBUVFUVIUWGUWBMZUVFMUVHBUVTUNZUWHUWIKZUVFUWHUVTBJZUWJUWBUWKUWGUWB
      UWKUVTUWAJZUVTDUOUPZBUWAUVTUQQURUVNUWGUWKUWJUSUWHUVNUWGMUWIUWKBUVTUTZVAUW
      ANJZUWBUVNUWGUWOUWBUVNUWANBVBVCUVTVDZVEUWGUWBVFVGVHVIUWGUWBUVFUVHUWISZUVF
      UWGUWBUWQUVFUWGUWBUVHUWIUVFUWGAUWAIGZJZUWIUWBUVHMUVFUWIAEUBZLZIGZJZENVJZV
      KZUVTUNZUWGUWSMUVFBUXEUVTUVFBUXEHUVEUXEBEACVLZVMVNVOUWGUWSUXFUXCUWSEUVTED
      VPZUXBUWRAUXHUXAUWAIUWTUVTVQVRUKVSZVCVTUWBUVHUWSUWBUVGUWRABUWAIUHUKWAWBWC
      WDWEWFWGWHUVFUWDUVIUVFUWDUVIUVFUWDMZUVHAUVTIGZJZDBUCZUVFUXMKUWDUXMUVFUXLU
      VFKZDBUXLUWKUXNUWKUXLUVFUWKKZSUWKUXNSUVFUWKUXLUXOUVFUWKUVFUWGMZUXLUXOSUVF
      UWKUWGUVFUWKUVTUVEJUWGBUVEUVTUQUVEUVTUVOWIWJWKUXPUXLUWIUXOUWGUXLUVEUVTUNZ
      UVFUWIUWGUXLUWSUXQUWGUXKUWRAUWGUWOUXKUWRUNZUWPUWOUWLUXRUWMUVTUWAWLWMWNWOU
      WGUWSUXFUXQUXIUVEUXEUVTUXGWPWQWRUVFUWIUXQBUVEUVTWSWTXAUVNUWGUWIUXOUSUVFUW
      NUVPVEXBXCWDUVFUWKXDXCXEWHXFVIUXJUVHADBUXKXGZJUXMUXJUVGUXSABXHJZUWDUVGUXS
      HUVFDBXHXIUVFUXTUVEXHJAFXJBUVEXHPQVEUKDABUXKXKXLXMXNXOXPXQWNXRUVFBUXDJZUV
      LUVFUYAUVEUXDJUVEUXEUXDUXGUXDNUNUXDUAHKZUXEUXDJUXCENXSUYBUXCENUCZAXHJUYCC
      EAXHXTYAUXCENYBYCUXDYDYEYFBUVEUXDPQUYAUVNUVLUXCUVLEBNUWTBHZUXBUVKAUYDUXAU
      VJIUWTBVQVRUKZYGYHWNYIUVMBUVEUNZUVEBUNZMUVFUVMUYFUYGUVIUVLUYFUVIUVNUYFUVL
      UVNUVIUYFABCYJXOUVLUVJIYKZJZUVNUYIUVLUYIKZUVLUWEUWFUYJUVKUAAUVJIYLUKULYMU
      YIUVJNJUVNUYHNUVJINYNUYHNHYONIYPYAYQBYRYSYTZUVAVCUVLUYGUVIUVLUXEBUNZUYGUV
      NUVLUYLUYKUXCUVLEBUYEVSXRUVEUXEBUXGWPUVBURYIBUVEUVCUVBUVD $.
      $([7-Oct-03]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( A relationship between the rank function and the cumulative hierarchy
       of sets function ` R1 ` .  Proposition 9.15(2) of [TakeutiZaring]
       p. 79.    This more general variant of ~ rankr1 expresses the class
       existence requirement as an antecedent.$)
    rankr1g $p |- ( A e. C -> ( B = ( rank ` A ) <->
                 ( -. A e. ( R1 ` B ) /\ A e. ( R1 ` suc B ) ) ) ) $=
      ( vx cv crnk cfv wceq cr1 wcel wn csuc wa wb fveq2 cleq2d eleq1 binegd
      bi2and bi2bid visset rankr1 vtoclg ) BDEZFGZHZUDBIGZJZKZUDBLIGZJZMZNBAFGZ
      HZAUGJZKZAUJJZMZNDACUDAHZUFUNULURUSUEUMBUDAFOPUSUIUPUKUQUSUHUOUDAUGQRUDAU
      JQSTUDBDUAUBUC $.
      $([8-Oct-03]$)
  $}

  ${
    ssrankr1.1 $e |- A e. V $.
    $( A relationship between an ordinal number less than or equal to a rank,
       and the cumulative hierarchy of sets ` R1 ` .  Proposition 9.15(3) of
       [TakeutiZaring] p. 79. $)
    ssrankr1 $p |- ( B e. On ->
                   ( B (_ ( rank ` A ) <-> -. A e. ( R1 ` B ) ) ) $=
      ( con0 wcel crnk cfv wss cr1 wn wa csuc wceq cleqid rankr1 mpbi pm3.26i
      wi rankon r1ord3 mpan2 imp ssd mtoi exp rankr1lem impbid ) BDEZBAFGZHZABI
      GZEZJZUHUJUMUHUJKZULAUIIGZEZUPJZAUILIGEZUIUIMUQURKUINAUICOPQUNUKUOAUHUJUK
      UOHZUHUIDEUJUSRASBUITUAUBUCUDUEABCUFUG $.
      $([8-Oct-03]$)
  $}

  ${
    $d x y A $.
    $( The value of the cumulative hierarchy of sets function expressed in
       terms of rank.  Definition 15.19 of [Monk1] p. 113. $)
    r1val2 $p |- ( A e. On -> ( R1 ` A ) = { x | ( rank ` x ) e. A } ) $=
      ( con0 wcel cv crnk cfv cr1 wss wn visset ssrankr1 word wb eloni rankon
      onord ordtri1 mpan2 syl bitr3d bicon4d biabrdv ) BCDZAEZFGZBDZABHGZUDUEUH
      DZUGUDBUFIZUIJUGJZUEBAKLUDBMZUJUKNZBOULUFMUMUFUEPQBUFRSTUAUBUC $.
      $([?]$)

    $( The value of the cumulative hierarchy of sets function expressed in
       terms of rank.  Theorem 15.18 of [Monk1] p. 113. $)
    r1val3 $p |- ( A e. On ->
                 ( R1 ` A ) = U. x e. A P~ { y | ( rank ` y ) e. x } ) $=
      ( con0 wcel cr1 cfv cv cpw ciun crnk cab r1val1 wceq wral wa onelon
      r1val2 pweq 3syl exp r19.21aiv iuneq2 syl eqtrd ) CDEZCFGACAHZFGZIZJZACBH
      KGUGEBLZIZJZACMUFUIULNZACOUJUMNUFUNACUFUGCEZUNUFUOPUGDEUHUKNUNCUGQBUGRUHU
      KSTUAUBACUIULUCUDUE $.
      $([?]$)
  $}

  ${
    rankel.1 $e |- B e. V $.
    $( The membership relation is inherited by the rank function.  Proposition
       9.16 of [TakeutiZaring] p. 79. $)
    rankel $p |- ( A e. B -> ( rank ` A ) e. ( rank ` B ) ) $=
      ( wcel crnk cfv csuc cr1 wn wceq wa cleqid rankr1g mpbii pm3.26d wss wi
      cpw con0 rankon r1suc ax-mp eleq2i elpw bitr ssel sylbi comm mtod wb
      ontri1 mp2an word onord ordsucsssuc rankid onsuc r1ord3 ssd mpi sylbir
      nsyl2 ) ABDZBAEFZGZHFZDZVDBEFZDZVCVGAVDHFZDZVCVKIZAVFDZVCVDVDJVLVMKVDLAVD
      BMNOVGVCVKVGBVJPZVCVKQVGBVJRZDVNVFVOBVDSDZVFVOJATZVDUAUBUCBVJCUDUEBVJAUFU
      GUHUIVIIZVHVDPZVGVHSDVPVSVRUJBTZVQVHVDUKULVSVHGZVEPZVGVHUMVDUMVSWBUJVHVTU
      NVDVQUNVHVDUOULWBBWAHFZDVGBCUPWBWCVFBWASDVESDWBWCVFPQVHVTUQVDVQUQWAVEURUL
      USUTUGVAVB $.
      $([9-Oct-03]$)
  $}

  ${
    $d x y A $.
    rankval3.1 $e |- A e. V $.
    $( The value of the rank function expressed recursively:  the rank of a set
       is the smallest ordinal number containing the ranks of all members of
       the set.  Proposition 9.17 of [TakeutiZaring] p. 79. $)
    rankval3 $p |- ( rank ` A ) =
                  |^| { x e. On | A. y e. A ( rank ` y ) e. x } $=
      ( crnk cfv cv wcel wral con0 crab cint csuc cr1 rankval wss visset
      rankid wi word eloni ordsucss syl rankon onsuc r1ord3 mpan syld ssel
      syl6 mpii r19.20sdv cpw r1suc eleq2d elpw dfss3 bitr syl6bb sylibrd
      ssrab2i ssintss ax-mp eqsstr rankel rgen wceq eleq2 biraldv onintss mp2
      eqssi ) CEFZBGZEFZAGZHZBCIZAJKZLZVMCVPMNFZHZAJKZLZVTACDOVSWCPWDVTPVRWBAJV
      PJHZVRVNVPNFZHZBCIZWBWEVQWGBCWEVQVNVOMZNFZHZWGVNBQRWEVQWJWFPZWKWGSWEVQWIV
      PPZWLWEVPTVQWMSVPUAVOVPUBUCWIJHWEWMWLSVOVNUDUEWIVPUFUGUHWJWFVNUIUJUKULWEW
      BCWFUMZHZWHWEWAWNCVPUNUOWOCWFPWHCWFDUPBCWFUQURUSUTVAVSWCVBVCVDVMJHVOVMHZB
      CIZVTVMPCUDWPBCVNCDVEVFVRWQAVMVPVMVGVQWPBCVPVMVOVHVIVJVKVL $.
      $([11-Oct-03]$)
  $}

  ${
    $d x y A $.
    $( Any class whose elements have bounded rank is a set.  Proposition 9.19
       of [TakeutiZaring] p. 80. $)
    bndrank $p |- ( E. x e. On A. y e. A ( rank ` y ) (_ x -> A e. V ) $=
      ( cv crnk cfv wss wral cvv wcel con0 csuc cr1 visset rankid wi word wb
      eloni rankon onord ordsucsssuc mpan syl suceloni onsuc r1ord3 sylbid
      ssel syl6 mpii r19.20sdv dfss3 fvex ssex sylbir r19.23aiv ) BDZEFZADZGZBC
      HZCIJZAKUTKJZVBURUTLZMFZJZBCHZVCVDVAVGBCVDVAURUSLZMFZJZVGURBNOVDVAVJVFGZV
      KVGPVDVAVIVEGZVLVDUTQZVAVMRZUTSUSQVNVOUSURTZUAUSUTUBUCUDVDVEKJZVMVLPZUTUE
      VIKJVQVRUSVPUFVIVEUGUCUDUHVJVFURUIUJUKULVHCVFGVCBCVFUMCVFVEMUNUOUPUJUQ $.
      $([20-Nov-03]$)

    $( The elements of a proper class have unbounded rank.  Exercise 2 of
       [TakeutiZaring] p. 80. $)
    unbndrank $p |- ( -. A e. V -> A. x e. On E. y e. A x e. ( rank ` y ) ) $=
      ( cv crnk cfv wcel wrex con0 wral cvv wn wss wb rankon ontri1 mpan
      biraldv ralnex syl6bb birexa rexnal bitr bndrank sylbir con1 ) ADZBDZEFZG
      ZBCHZAIJZCKGZULLZUIUGMZBCJZAIHZUMUQUKLZAIHUNUPURAIUGIGZUPUJLZBCJURUSUOUTB
      CUIIGUSUOUTNUHOUIUGPQRUJBCSTUAUKAIUBUCABCUDUEUF $.
      $([21-Nov-03]$)
  $}

  ${
    rankpw.1 $e |- A e. V $.
    $( The rank of a power set.  Part of Exercise 30 of [Enderton] p. 207. $)
    rankpw $p |- ( rank ` P~ A ) = suc ( rank ` A ) $=
      ( crnk cfv csuc cpw wceq cr1 wcel wn wa wss cleqid rankr1 pm3.26bd ax-mp
      pwid ssel mpi mto zfpowcl elpw mtbir con0 rankon r1suc eleq2i rankid
      eleqtr mpbi sspwb sseqtr4 mpbir onsuc eleqtrr pm3.2i cleqcomi ) ACDZEZAFZ
      CDZUSVAGUTUSHDZIZJZUTUSEHDZIZKVDVFVCUTURHDZFZIZVIUTVGLZVJAVGIZURURGZVKJZU
      RMVLVMAVBIAURBNOPVJAUTIVKABQUTVGARSTUTVGABUAZUBUCVBVHUTURUDIVBVHGAUEZURUF
      PZUGUCUTVBFZVEUTVQIUTVBLUTVHVBAVGLZUTVHLAVHIVRAVBVHABUHVPUIAVGBUBUJAVGUKU
      JVPULUTVBVNUBUMUSUDIVEVQGURVOUNUSUFPUOUPUTUSVNNUMUQ $.
      $([22-Nov-03]$)
  $}

  ${
    rankss.1 $e |- B e. V $.
    $( The subset relation is inherited by the rank function.  Exercise 1 of
       [TakeutiZaring] p. 80. $)
    rankss $p |- ( A (_ B -> ( rank ` A ) (_ ( rank ` B ) ) $=
      ( cpw wcel crnk cfv wss zfpowcl rankel cvv wb elpw2g ax-mp csuc rankpw
      eleq2i con0 rankon onsssuc mp2an bitr4 3imtr3 ) ABDZEZAFGZUDFGZEZABHZUFBF
      GZHZAUDBCIJBKEUEUILCABKMNUHUFUJOZEZUKUGULUFBCPQUFREUJREUKUMLASBSUFUJTUAUB
      UC $.
      $([25-Nov-03]$)
  $}

  ${
    $d x y z A $.
    ranksn.1 $e |- A e. V $.
    $( The rank of a singleton.  Theorem 15.17(v) of [Monk1] p. 112. $)
    ranksn $p |- ( rank ` { A } ) = suc ( rank ` A ) $=
      ( vy vx cv crnk cfv wcel csn wral con0 crab cint csuc wb wi wal wceq
      df-ral elsn birim bial fveq2 eleq1d ceqsalv 3bitr a1i birabi inteqi snex
      rankval3 rankon onsucmin ax-mp 3eqtr4 ) CEZFGZDEZHZCAIZJZDKLZMAFGZURHZDKL
      ZMZUTFGVCNZVBVEVAVDDKVAVDOURKHVAUPUTHZUSPZCQUPARZUSPZCQVDUSCUTSVIVKCVHVJU
      SCATUAUBUSVDCABVJUQVCURUPAFUCUDUEUFUGUHUIDCUTAUJUKVCKHVGVFRAULDVCUMUNUO
      $.
      $([?]$)

    $( The rank of a union.  Part of Theorem 15.17(iv) of [Monk1] p. 112. $)
    rankuni $p |- ( rank ` U. A ) = U. x e. A ( rank ` x ) $=
      ( vy vz cuni crnk cfv cv ciun wcel wral con0 crab cint zfun2 rankval3
      wss wa rankon a1i rgen fvex iunon ax-mp wel wrex eluni2 ax-17 hbiu1 hbel
      ssiun2 ssd visset rankel syl5 r19.23ai sylbi pm3.2i wceq eleq2 biraldv
      elrab mpbir intss eqsstr elssuni rankss syl iunss eqssi ) BFZGHZABAIZGHZJ
      ZVMDIZGHZEIZKZDVLLZEMNZOZVPEDVLBCPZQVPWBKZWCVPRWEVPMKZVRVPKZDVLLZSWFWHVOM
      KZABLWFWIABWIVNBKZVNTUAUBABVOCVNGUCUDUEWGDVLVQVLKDAUFZABUGWGAVQBUHWKWGABA
      EEVRVPVSVRKAUIAEBVOUJUKWJVRVOKWGWKWJVOVPVRABVOULUMVQVNAUNUOUPUQURUBUSWAWH
      EVPMVSVPUTVTWGDVLVSVPVRVAVBVCVDVPWBVEUEVFVPVMRVOVMRZABLWLABWJVNVLRWLVNBVG
      VNVLWDVHVIUBABVOVMVJVDVK $.
      $([?]$)

    $( Upper bound of the rank of a union.  Part of Exercise 30 of [Enderton]
       p. 207. $)
    rankuniss $p |- ( rank ` U. A ) (_ ( rank ` A ) $=
      ( vx cuni crnk cfv cv ciun rankuni wss wral wcel rankel rankon onelss
      syl rgen iunss mpbir eqsstr ) ADEFCACGZEFZHZAEFZCABIUCUDJUBUDJZCAKUECAUAA
      LUBUDLUEUAABMUDUBANOPQCAUBUDRST $.
      $([?]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    rankun.1 $e |- A e. V $.
    rankun.2 $e |- B e. V $.
    $( The rank of the union of two sets.  Theorem 15.17(iii) of [Monk1]
       p. 112. $)
    rankun $p |- ( rank ` ( A u. B ) ) = ( ( rank ` A ) u. ( rank ` B ) ) $=
      ( vx vz vy cun crnk cfv cv wcel wral wel wi con0 crab cint unex rankval3
      eleq2i visset elintrab bitr wo elun rankel elun1 syl elun2 jaoi sylbi
      rgen rankon onun wceq eleq2 biraldv bi2imd rcla4v mpi ssriv wss ssun1
      rankss ax-mp ssun2 unssi eqssi ) ABHZIJZAIJZBIJZHZEVKVNEKZVKLZFKZIJZGKZLZ
      FVJMZEGNZOZGPMZVOVNLZVPVOWAGPQRZLWDVKWFVOGFVJABCDSZTUAWAGVOPEUBUCUDWDVRVN
      LZFVJMZWEWHFVJVQVJLVQALZVQBLZUEWHVQABUFWJWHWKWJVRVLLWHVQACUGVRVLVMUHUIWKV
      RVMLWHVQBDUGVRVMVLUJUIUKULUMWDVNPLWIWEOZVLVMAUNBUNUOWCWLGVNPVSVNUPZWAWIWB
      WEWMVTWHFVJVSVNVRUQURVSVNVOUQUSUTVAVAULVBVLVMVKAVJVCVLVKVCABVDAVJWGVEVFBV
      JVCVMVKVCBAVGBVJWGVEVFVHVI $.
      $([?]$)

    $( The rank of an unordered pair.  Part of Exercise 30 of [Enderton]
       p. 207. $)
    rankpr $p |- ( rank ` { A , B } ) = suc ( ( rank ` A ) u. ( rank ` B ) ) $=
      ( cpr crnk cfv csuc cun csn df-pr fveq2i snex rankun wceq ranksn uneq12
      mp2an 3eqtr word rankon onord ordsucun eqtr4 ) ABEZFGZAFGZHZBFGZHZIZUGUII
      HZUFAJZBJZIZFGUMFGZUNFGZIZUKUEUOFABKLUMUNAMBMNUPUHOUQUJOURUKOACPBDPUPUHUQ
      UJQRSUGTUITULUKOUGAUAUBUIBUAUBUGUIUCRUD $.
      $([?]$)
  $}

  $( Any set is a subset of the hierarchy of its rank. $)
  r1rankid $p |- ( A e. B -> A (_ ( R1 ` ( rank ` A ) ) ) $=
    ( wcel crnk cfv cr1 cpw wss csuc wn wceq wa cleqid rankr1g mpbii pm3.27d
    con0 rankon r1suc ax-mp eleq2i sylib elpwg mpbid ) ABCZAADEZFEZGZCZAUGHUEAU
    FIFEZCZUIUEAUGCJZUKUEUFUFKULUKLUFMAUFBNOPUJUHAUFQCUJUHKARUFSTUAUBAUGBUCUD
    $.
    $([9-Jan-04]$)

  ${
    $d x y z A $.
    $( The rank of an ordinal number is itself.  Proposition 9.18 of
       [TakeutiZaring] p. 79 and its converse. $)
    rankonid $p |- ( A e. On <-> ( rank ` A ) = A ) $=
      ( vx vy vz con0 wcel crnk cfv wceq cv fveq2 id cleq12d weq wral wel wi
      wal wss crab cint wb eleq1 r19.20si r19.15 syl dfss3 syl6bbr birabsdv
      inteqd visset rankval3 syl5eq intmin cleqcomd sylan9eqr exp df-ral
      syl5ibr tfis2 vtoclga rankon mpbii impbi ) AEFZAGHZAIZBJZGHZVHIZVGBAEVHAI
      ZVIVFVHAVHAGKVKLMVJCJZGHZVLIZBCBCNZVIVMVHVLVHVLGKVOLMVHEFZVNCVHOZVJCBPVNQ
      CRVPVQVJVQVPVIVHDJZSZDETZUAZVHVQVMVRFZCVHOZDETZUAWAVIVQWDVTVQWCVSDEVQWCCD
      PZCVHOZVSVQWBWEUBZCVHOWCWFUBVNWGCVHVMVLVRUCUDWBWECVHUEUFCVHVRUGUHUIUJDCVH
      BUKULUMVPVHWADVHEUNUOUPUQVNCVHURUSUTVAVGVFEFVEAVBVFAEUCVCVD $.
      $([25-Nov-03]$)
  $}

  ${
    $d x y A $.
    $( The rank of the hierarchy of an ordinal number is itself. $)
    rankr1id $p |- ( A e. On <-> ( rank ` ( R1 ` A ) ) = A ) $=
      ( vx vy con0 wcel cr1 cfv crnk wceq cv fveq2 fveq2d id cleq12d wss wa
      crab cint wi wral r1ord3 exp r19.21aiv ssrab2 sylibr ssintss syl intmin
      sseq2d mpbird cvv fvex rankval2 ax-mp sseq1i rankonid visset r1rankid
      mpbii rankss sseq1 mpbid sylbi jca eqss vtoclga rankon eleq1 impbi ) ADEZ
      AFGZHGZAIZBJZFGZHGZVNIZVMBADVNAIZVPVLVNAVRVOVKHVNAFKLVRMNVNDEZVPVNOZVNVPO
      ZPVQVSVTWAVSVOCJZFGOZCDQZRZVNOZVTVSWFWEVNWBOZCDQZRZOZVSWHWDOZWJVSWGWCSZCD
      TWKVSWLCDVSWBDEWLVNWBUAUBUCWGWCCDUDUEWHWDUFUGVSVNWIWECVNDUHUIUJVPWEVNVOUK
      EVPWEIVNFULZCVOUKUMUNUOUEVSVNHGZVNIZWAVNUPWOWNVPOZWAWOVNVOOZWPWOVNWNFGZOZ
      WQVNUKEWSBUQVNUKURUNWOWRVOVNWNVNFKUIUSVNVOWMUTUGWNVNVPVAVBVCVDVPVNVEUEVFV
      MVLDEVJVKVGVLADVHUSVI $.
      $([25-Nov-03]$)
  $}

  ${
    $d x y z A $.
    ranklon.1 $e |- A e. V $.
    $( The rank of a set is the least ordinal number that is strictly greater
       than the ranks of all members of the set.  Exercise 9.1 of [Jech] p. 72.
       Also a special case of Theorem 7V(b) of [Enderton] p. 204. $)
    ranklon $p |- ( rank ` A ) = U. x e. A suc ( rank ` x ) $=
      ( vy crnk cfv cv csuc ciun cr1 wss wcel wi ax-17 hbiu1 hbfv dfssf visset
      rankid ssiun2 con0 rankon onsuc wral a1i rgen fvex sucex iunon ax-mp
      r1ord3 mp2an syl ssd mpi mpgbir rankss crab cint mpan ssrab2i ssintss
      cvv wceq rankval2 intmin 3sstr4 sstri rankel onsucss sylib iunss mpbir
      eqssi ) BEFZABAGZEFZHZIZVOVSJFZEFZVSBVTKZVOWAKWBVPBLZVPVTLZMAADBVTDGZBLAN
      ADVSJWEJLANADBVROPQWCVPVRJFZLWDVPARSWCWFVTVPWCVRVSKZWFVTKZABVRTVRUALZVSUA
      LZWGWHMVQVPUBZUCZWIABUDWJWIABWIWCWLUEUFABVRCVQVPEUGUHUIUJZVRVSUKULUMUNUOU
      PBVTVSJUGZUQUJVTWEJFKZDUAURZUSZVSWEKZDUAURZUSZWAVSWSWPKWQWTKWRWODUAWJWEUA
      LWRWOMWMVSWEUKUTVAWSWPVBUJVTVCLWAWQVDWNDVTVCVEUJWJVSWTVDWMDVSUAVFUJVGVHVS
      VOKVRVOKZABUDXAABWCVQVOLXAVPBCVIVQVOWKBUBVJVKUFABVRVOVLVMVN $.
      $([?]$)
  $}

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, is a set. $)
    scottex $p |- { x e. A | A. y e. A ( rank ` x ) (_ ( rank ` y ) } e. V $=
      ( vz vw c0 wceq cv crnk cfv wss wral crab cvv wcel zfnull eleq1 mpbiri
      zfausrabg syl wn wex nnull hbra1 ax-17 hbrab hbel wi ra4 comm a1d
      r19.21aiv ssrab2 sylibr con0 wrex rankon weq fveq2 sseq1d elrab pm3.27bd
      rgen sseq2 biraldv rcla4ev mp2an bndrank ax-mp ssex 19.23ai sylbi
      pm2.61i ) CFGZAHZIJZBHZIJZKZBCLZACMZNOZVNCNOZWBVNWCFNOPCFNQRVTACNSTVNUAVQ
      COZBUBWBBCUCWDWBBBDDWANVTBADCVSBCUDDHZCOBUEUFWENOBUEUGWDWAVSACMZKZWBWDVTV
      SUHZACLWGWDWHACWDWHVOCOVTWDVSVSBCUIUJUKULVTVSACUMUNWAWFEHZIJZWEKZEWFLZDUO
      UPZWFNOVRUOOWJVRKZEWFLZWMVQUQWNEWFWIWFOWICOWNVSWNAWICAEURVPWJVRVOWIIUSUTV
      AVBVCWLWODVRUOWEVRGWKWNEWFWEVRWJVDVEVFVGDEWFVHVIVJTVKVLVM $.
      $([21-Dec-03]$)
  $}

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, contains at
       least one representative with the property, if there is one.  In other
       words, the collection is empty iff no set has the property (i.e. ` A `
       is empty). $)
    scott0 $p |- ( A = (/) <->
               { x e. A | A. y e. A ( rank ` x ) (_ ( rank ` y ) } = (/) ) $=
      ( vz c0 wceq cv crnk cfv wss wral crab rabeq rab0 syl6eq wn wrex ciin
      cab wcel wex nnull hbre1 cleqid ra4e mpan2 19.23ai sylbi wa fvex cleq1
      biland cla4ev 19.22i 19.11 sylibr df-rex biex 3imtr4 syl noel3 cint con0
      wi hbab1 ax-17 dfssf abid rankon eleq1 mpbiri a1i r19.23aiv mpgbir onint
      mpan dfiin2 eleq1i wel hbii1 hbeq birexd elabg ibi sseq1 ssid weq fveq2
      sseq1d rcla4ev iinss syl5bi r19.21aiv r19.22si 3syl rnoel3 a3i impbi ) CE
      FZAGZHIZBGZHIZJZBCKZACLZEFZWSXFXEAELEXEACEMXEANOWSXGWSPZXEACQZXGPXHACXARZ
      XBXAFZACQZBSZTZXJXAFZACQZXIXHXMEFPZXNXHXLBUAZXQXHXAXAFZACQZXRXHWTCTZAUAXT
      ACUBYAXTAXSACUCYAXSXTXAUDXSACUEUFUGUHYAXSUIZAUAZYAXKUIZAUAZBUAZXTXRYCYDBU
      AZAUAYFYBYGAYDYBBXAWTHUJZXKXKXSYAXBXAXAUKULUMUNYDBAUOUPXSACUQXLYEBXKACUQU
      RUSUTXLBVAUPXQXMVBZXMTZXNXMVCJZXQYJYKXBXMTZXBVCTZVDBBDXMVCXLBDVEDGVCTBVFV
      GYLXLYMXLBVHXKYMACXKYMVDYAXKYMXAVCTWTVIXBXAVCVJVKVLVMUHVNXMVOVPXJYIXMABCX
      AYHVQVRUPUTXNXPXLXPBXJXMXBXJFXKXOACADDXBXJDBVSAVFADCXAVTWAXBXJXAUKWBWCWDX
      OXEACXOXDBCXOXJXCJZXDXBCTZXJXAXCWEYOXDACQZYNYOXCXCJZYPXCWFXDYQAXBCABWGXAX
      CXCWTXBHWHWIWJUFACXAXCWKUTWLWMWNWOXEACWPUPWQWR $.
      $([22-Dec-03]$)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d w C $.  $d w D $.
    cplem1.1 $e |- C = { y e. B | A. z e. B ( rank ` y ) (_ ( rank ` z ) } $.
    cplem1.2 $e |- D = U. x e. A C $.
    $( Lemma for the Collection Principle ~ cp . $)
    cplem1 $p |- A. x e. A ( -. B = (/) -> -. ( B i^i D ) = (/) ) $=
      ( vw c0 wceq wn cin wi cv wcel wex wa crnk cfv wss wral crab ssrab
      eqsstr ssi a1i ciun ssiun2 sseq2i sylibr ssd jcad inelcm syl6 19.23adv
      scott0 cleq1i bitr4 bineg nnull bitr syl5ib rgen ) EKLZMZEGNKLMZOADAPDQZJ
      PZFQZJRZVHVGVIVKVHJVIVKVJEQZVJGQZSVHVIVKVMVNVKVMOVIFEVJFBPTUACPTUAUBCEUCZ
      BEUDZEHVOBEUEUFUGUHVIFGVJVIFADFUIZUBFGUBADFUJGVQFIUKULUMUNVJEGUOUPUQVGFKL
      ZMVLVFVRVFVPKLVRBCEURFVPKHUSUTVAJFVBVCVDVE $.
      $([31-Dec-03]$)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d w C $.  $d w D $.
    cplem2.1 $e |- A e. V $.
    $( Lemma for the Collection Principle ~ cp . $)
    cplem2 $p |- E. y A. x e. A ( -. B = (/) -> -. ( B i^i y ) = (/) ) $=
      ( vz vw c0 wceq wn cv crnk cfv wss wral crab ciun cin wi wex cleqid
      cplem1 scottex iunex wel ax-17 hbiu1 hbeq ineq2 cleq1d binegd bilimd
      birald cla4ev ax-mp ) DHIJZDACFKLMGKLMNGDOFDPZQZRZHIZJZSZACOZUPDBKZRZHIZJ
      ZSZACOZBTAFGCDUQURUQUAURUAUBVIVCBURACUQEFGDUCUDVDURIZVHVBACAFBVDURFBUEAUF
      ABCUQUGUHVJVGVAUPVJVFUTVJVEUSHVDURDUIUJUKULUMUNUO $.
      $([31-Dec-03]$)
  $}

  ${
    $d ph z w $. $d x y z w $.
    $( Collection Principle.  This remarkable theorem scheme is in effect a
       very strong generalization of the Axiom of Replacement.  The proof
       makes use of Scott's trick ~ scottex that collapses a proper class into
       a set of minimum rank.  The wff ` ph ` can be thought of as
       ` ph ( x , y ) ` .  Scheme "Collection Principle" of [Jech] p. 72. $)
    cp $p |- E. w A. x e. z ( E. y ph -> E. y e. w ph ) $=
      ( cab c0 wceq wn cv cin wi wral wex wrex visset cplem2 noel3 wcel wel wa
      elin abid biran ancom 3bitr biex hbab1 ax-17 hbin nnullf df-rex 3bitr4
      bi2im biral mpbi ) ACFZGHIZUQEJZKZGHIZLZBDJZMZENACNZACUSOZLZBVCMZENBEVCUQ
      DPQVDVHEVBVGBVCURVEVAVFACRCJZUTSZCNCETZAUAZCNVAVFVJVLCVJVIUQSZVKUAAVKUAVL
      VIUQUSUBVMAVKACUCUDAVKUEUFUGCDUTCDUQUSACDUHDETCUIUJUKACUSULUMUNUOUGUP $.
      $([31-Dec-03]$)
  $}

  ${
    $d ph z w $. $d x y z w $.
    $( A very strong generalization of the Axiom of Replacement (compare
       ~ zfrep6 ), derived from the Collection Principle ~ cp .  Its strength
       lies in the rather profound fact that ` ph ( x , y ) ` does not have to
       be a "function-like" wff, as it does in the standard Axiom of
       Replacement.  This theorem is sometimes called the Boundedness Axiom. $)
    cp2 $p |- ( A. x e. z E. y ph -> E. w A. x e. z E. y e. w ph ) $=
      ( wex cv wral wrex wi cp r19.20 19.22i ax-mp 19.37v mpbi ) ACFZBDGZHZACEG
      IZBRHZJZEFZSUAEFJQTJBRHZEFUCABCDEKUDUBEQTBRLMNSUAEOP $.
      $([2-Jan-04]$)
  $}

  ${
    $d x y z A $.
    kardex.1 $e |- A e. V $.
    $( The collection of all sets equinumerous to a set ` A ` and having least
       possible rank is a set.  This is the part of the justification of the
       definition of kard of [Enderton] p. 222. $)
    kardex $p |- { x | ( x ~~ A /\
                  A. y ( y ~~ A -> ( rank ` x ) (_ ( rank ` y ) ) ) } e. V $=
      ( vz cv crnk cfv wss cen wbr cab wral crab wi wal wa cvv wcel df-rab
      visset breq1 elab df-ral birim bial bitr bi2an biabi eqtr scottex
      eqeltrr ) AFZGHBFZGHIZBEFZCJKZELZMZAURNZUMCJKZUNCJKZUOOZBPZQZALZRUTUMURSZ
      USQZALVFUSAURTVHVEAVGVAUSVDUQVAEUMAUAUPUMCJUBUCUSUNURSZUOOZBPVDUOBURUDVJV
      CBVIVBUOUQVBEUNBUAUPUNCJUBUCUEUFUGUHUIUJABURUKUL $.
      $([?]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d z C $.  $d z D $.
    karden.1 $e |- A e. V $.
    karden.2 $e |- B e. V $.
    karden.3 $e |- C =
       { x | ( x ~~ A /\ A. y ( y ~~ A -> ( rank ` x ) (_ ( rank ` y ) ) ) } $.
    karden.4 $e |- D =
       { x | ( x ~~ B /\ A. y ( y ~~ B -> ( rank ` x ) (_ ( rank ` y ) ) ) } $.
    $( If we allow the Axiom of Regularity, we can avoid the Axiom of Choice by
       defining the cardinal number of a set as the set of all sets
       equinumerous to it and having least possible rank.  This theorem proves
       the equinumerosity relationship for this definition (compare ~ carden ).
       The hypotheses correspond to the definition of kard of [Enderton] p. 222
       (which we don't define separately since currently we do not use it
       elsewhere).  This theorem along with ~ kardex justify the definition of
       kard. $)
    karden $p |- ( C = D <-> A ~~ B ) $=
      ( vz vw wceq cen wbr cv crnk cfv wss cab wral wrex crab c0 wn wex cvv
      wcel enrefg ax-mp breq1 cla4ev noel3 mpbir scott0 mtbi rnoel3 mpbi wi
      wal wa pm3.26 a1i wb cleq12i cleq2ab bitr weq fveq2 sseq1d bilimd bialdv
      bi2and bi2bid a4b1 sylbi syl6bi jcad entr ensymg sylan syl6 visset elab
      df-ral birim bial bi2an syl5ib exp3a r19.23adv mpi enen2 mpan birimd
      biabdv 3eqtr4g impbi ) EFMZCDNOZWSKPZQRZBPZQRZSZBLPZCNOZLTZUAZKXHUBZWTXIK
      XHUCUDMZUEXJXHUDMZXKXLUEXGLUFZCCNOZXMCUGUHZXNGCUGUIUJXGXNLCGXFCCNUKULUJXG
      LUMUNKBXHUOUPXIKXHUQURWSXIWTKXHWSXAXHUHZXIWTWSXACNOZXCCNOZXEUSZBUTZVAZWTX
      PXIVAWSYAXQXADNOZVAWTWSYAXQYBYAXQUSWSXQXTVBVCWSYAYBXCDNOZXEUSZBUTZVAZYBWS
      APZCNOZXRYGQRZXDSZUSZBUTZVAZYGDNOZYCYJUSZBUTZVAZVDZAUTZYAYFVDZWSYMATZYQAT
      ZMYSEUVAFUVBIJVEYMYQAVFVGYRYTAKAKVHZYMYAYQYFUVCYHXQYLXTYGXACNUKUVCYKXSBUV
      CYJXEXRUVCYIXBXDYGXAQVIVJZVKVLVMUVCYNYBYPYEYGXADNUKUVCYOYDBUVCYJXEYCUVDVK
      VLVMVNVOVPYBYEVBVQVRCXANOZYBWTXQCXADVSXOXQUVEUSGXACUGVTUJWAWBXPXQXIXTXGXQ
      LXAKWCXFXACNUKWDXIXCXHUHZXEUSZBUTXTXEBXHWEUVGXSBUVFXRXEXGXRLXCBWCXFXCCNUK
      WDWFWGVGWHWIWJWKWLWTUVAUVBEFWTYMYQAWTYHYNYLYPDUGUHZWTYHYNVDHCDYGUGWMWNWTY
      KYOBWTXRYCYJUVHWTXRYCVDHCDXCUGWMWNWOVLVMWPIJWQWR $.
      $([?]$)
  $}


