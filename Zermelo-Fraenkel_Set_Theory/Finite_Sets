$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for Pigeonhole Principle. $)
  phplem1 $p |- ( B e. A -> ( { B } u. ( A \ { B } ) ) = A ) $=
    ( wcel csn cdif cun wss wceq snssi undif3 sylib cleqcomd ) BACZABDZANEFZMNA
    GAOHBAINAJKL $.
    $([25-May-98]$)

  $( Lemma for Pigeonhole Principle. $)
  phplem2 $p |- ( ( A e. om /\ B e. A ) ->
             ( { A } u. ( A \ { B } ) ) = ( suc A \ { B } ) ) $=
    ( com wcel wa csn cin c0 wceq cdif cun csuc word wn nordeq disjsn2 syl
    nnord sylan undif4 df-suc uncom eqtr difeq1 ax-mp syl6eqr ) ACDZBADZEAFZBFZ
    GHIZUIAUJJKZALZUJJZIAMZUHUKUGUOUHEABINUKABOABPQARSUKULUIAKZUJJZUNUIAUJTUMUP
    IUNUQIUMAUIKUPAUAAUIUBUCUMUPUJUDUEUFQ $.
    $([25-May-98]$)

  ${
    $d f A $.  $d f B $.  $( ` f ` is dummy. $)
    phplem3.1 $e |- A e. V $.
    phplem3.2 $e |- B e. V $.
    phplem3 $p |- ( ( A e. om /\ B e. A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wa csn cdif cun cen wbr csuc wceq phplem1 adantl phplem2
      breq12d cin c0 wss word nnord orddisj syl sseq2d difss ssrin ax-mp mpbii
      ss0 incom syl5eq indif0 jctil cop wf1o f1osn snex f1oen cvv ssexi enrefg
      pm3.2i enun adantr mpbid ) AEFZBAFZGZBHZAVKIZJZAHZVLJZKLZAAMVKIZKLVJVMAVO
      VQKVIVMANVHABOPABQRVHVPVIVHVKVNKLZVLVLKLZGZVKVLSTNZVNVLSZTNZGZGVPVHWDVTVH
      WCWAVHVLVNSZTWBVHWETUAZWETNVHWEAVNSZUAZWFVHWGTWEVHAUBWGTNAUCAUDUEUFVLAUAW
      HAVKUGZVLAVNUHUIUJWEUKUEVNVLULUMVKAUNUOVRVSVKVNBAUPHZUQVRBADCURVKVNWJBUSU
      TUIVLVAFVSVLACWIVBVLVAVCUIVDUOVKVNVLVLVEUEVFVG $.
      $([11-Jun-98]$)

    $( Lemma for Pigeonhole Principle. $)
    phplem4 $p |- ( ( A e. om /\ B e. suc A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wceq wo csuc csn cdif cen wbr wa andi phplem3 word nnord
      orddif syl sneq difeq2 cleqcoms sylan9eq breq2 cvv enrefg ax-mp mpbii
      jaoi sylbi elsuci sylan5 ) AEFZBAFZBAGZHZAAIZBJZKZLMZBURFUNUQNUNUONZUNUPN
      ZHVAUNUOUPOVBVAVCABCDPVCAUTGZVAUNUPAURAJZKZUTUNAQAVFGARASTVFUTGZABABGVEUS
      GVGABUAVEUSURUBTUCUDVDAALMZVAAUTALUEAUFFVHCAUFUGUHUITUJUKBAULUM $.
      $([26-May-98]$)

    $( Lemma for Pigeonhole Principle. $)
    phplem5 $p |- ( ( A e. om /\ B e. om ) -> ( suc A ~~ suc B -> A ~~ B ) ) $=
      ( vf com wcel wa csuc cv wf1o wex cen wbr anandir cfv csn cdif entr cima
      wceq wb word nnord orddif imaeq2 3syl wfn ccnv f1o4 pm3.26bd fnsnfv
      sucid mpan2 difeq2 crn cdm imadmrn cleqcomi a1i wfo wf1 df-f1o pm3.27bd
      forn syl fndm 3eqtr3d difeq1 wfun f1o3 imadif 3eqtr4rd sylan9eq breq2
      wss cres tz7.23b jctir f1ores f1oen adantl mpbid fvex phplem4 eleq2d
      fnfvrn sylan5 sucex difss ssexi ener ersym syl2an sylbi exp 19.23adv
      bren syl5ib ) AFGZBFGZHZAIZBIZEJZKZELABMNZXCXDMNXBXFXGEXBXFXGXBXFHWTXFHZX
      AXFHZHXGWTXAXFOAXDAXEPZQZRZMNZXLBMNZXGXHXIAXLBSXHAXEATZMNZXMXHXOXLUAXPXMU
      BWTXFXOXEXCAQZRZTZXLWTAUCAXRUAXOXSUAAUDAUEAXRXEUFUGXFXEXCTZXKRZXTXEXQTZRZ
      XLXSXFXEXCUHZXKYBUAZYAYCUAXFYDXEUIZXDUHXCXDXEUJUKZYDAXCGZYEXCAXEULACUMZUN
      XKYBXTUOUGXFXDXTUAXLYAUAXFXEUPZXEXEUQZTZXDXTYJYLUAXFYLYJXEURUSUTXFXCXDXEV
      AZYJXDUAXFXCXDXEVBZYMXCXDXEVCZVDXCXDXEVEZVFXFYDYKXCUAYLXTUAYGXCXEVGYKXCXE
      UFUGVHXDXTXKVIVFXFYFVJZXSYCUAXFYMYQXCXDXEVKZVDXCXQXEVLVFVMVNXOXLAMVOVFXFX
      PWTXFYNAXCVPZHAXOXEAVQZKXPXFYNYSXFYNYMYOUKAVRVSXCXDAXEVTAXOYTCWAUGWBWCXIB
      XLMNZXNXAXJXDGZUVAXFBXJDAXEWDWEXFXJYJGZUVBXFYMUVCUVBUBXFYMYQYRUKYMYJXDXJY
      PWFVFXFYDUVCYGYDYHUVCXCAXEWGYIUNVFWCWHBXLMDXLXDBDWIZXDXKWJWKWLWMVFWNWOWPW
      QXCXDEUVDWRWS $.
      $([28-May-98]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Two equinumerous natural numbers are equal.  Compare Corollary 6E
       of Enderton, p. 136. $)
    nneneq $p |- ( ( A e. om /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
      ( vz vx vy vw com wcel wa cen wbr wceq wi cv wral weq c0 csuc breq1
      cleq1 bi2imd biraldv zfnull visset ener ersym en0 cleqcom bitr sylib a1i
      rgen wrex wo wb breq2 cleq2 bi2bid mpbiri biimpd ax-17 hbra1 hban
      phplem5 syl4d exp a2d ra4 syl5 imp suceq syl8 biimprcd syl6 r19.23ad
      jaod nn0suc syl7 r19.21adv rcbvalv syl6ib finds rcla4v syl enrefg syl5bi
      comm adantr impbid ) AGHZBGHZIABJKZABLZWJWKWLWMMZWJACNZJKZAWOLZMZCGOZWKWN
      MDNZWOJKZDCPZMZCGOQWOJKZQWOLZMZCGOENZWOJKZECPZMZCGOZXGRZWOJKZXLWOLZMZCGOZ
      WSDEAWTQLZXCXFCGXQXAXDXBXEWTQWOJSWTQWOTUAUBDEPZXCXJCGXRXAXHXBXIWTXGWOJSWT
      XGWOTUAUBWTXLLZXCXOCGXSXAXMXBXNWTXLWOJSWTXLWOTUAUBWTALZXCWRCGXTXAWPXBWQWT
      AWOJSWTAWOTUAUBXFCGXFWOGHZXDWOQJKZXEQWOJUCCUDZUEUFYBWOQLXEWOUGWOQUHUIUJUK
      ULXGGHZXKXLFNZJKZXLYELZMZFGOXPYDXKYHFGYDXKYEQLZYEWORZLZCGUMZUNZYHYEGHYDXK
      YMYHMYDXKIZYIYHYLYIYHMYNYIYFYGYIYFYGUOXLQJKZXLQLZUOYIYFYOYGYPYEQXLJUPYEQX
      LUQURXLUGUSUTUKYNYKYHCGYDXKCYDCVAXJCGVBVCYHCVAYNYAXLYJJKZXLYJLZMZYKYHMYNY
      AYQXIYRYDXKYAYQXIMZMZYDYAXJMUVAXKYDYAXJYTYDYAXJYTMYDYAIYQXHXIXGWOEUDYCVDV
      EVFVGXJCGVHVIVJXGWOVKVLYKYHYSYKYFYQYGYRYEYJXLJUPYEYJXLUQUAVMVNVOVPVFCYEVQ
      VRVSYHXOFCGFCPYFXMYGXNYEWOXLJUPYEWOXLUQUAVTWAWBWRWNCBGWOBLWPWLWQWMWOBAJUP
      WOBAUQUAWCWDVJWJWMWLMWKWMWJWLWMAAJKWLWJABAJUPAGWEWFWGWHWI $.
      $([28-May-98]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Pigeonhole Principle.  A natural number is not equinumerous to a
       proper subset of itself.  Enderton, p. 134. $)
    php $p |- ( ( A e. om /\ B (. A ) -> -. A ~~ B ) $=
      ( vx vy com wcel wpss cen wbr wn wi wa cv csuc wceq wrex c0 nn0suc
      orcanai wss sspsstr nullss mpan nullpss sylib sylan5 psseq2 breq1 binegd
      bi2imd cdom wex csn cdif domentr wb cin disjsn disj3 bitr3 sseq1 sylbi
      ssdif syl5bir cvv visset sucex difss ssexi ssdom2g ax-mp syl6 pssss syl5
      imp phplem4 ensymg syl syl2an exp43 com4r 19.23aiv pssnel mpcom endomtr
      tz7.23b jctir sbth exp comm pm2.16d nneneq peano2b sylanb anabss word
      nnord nordeq sucid mpan2 mtbird pm2.43d r19.23aiv ) AEFZBAGZABHIZJZXDXEXG
      XDXEXEXGKZXDXELACMZNZOZCEPZXHXDAQOZJZXLXEXDXMXLCARSXEQAGZXNQBTXEXOQBAUABU
      BUCAUDUEUFXKXHCEXKXIEFZXHXKXHBXJGZXJBHIZJZKXPXKXEXQXGXSAXJBUGXKXFXRAXJBHU
      HUIUJXQXPXSXQXPXSXQXPBXIUKIZXPXSKDMZXJFZYABFJZLZDULXQXPXTKZYDXQYEKZDYBYCY
      FYCXQXPYBXTYCXQXPYBXTBXJYAUMZUNZUKIZYHXIHIZXTYCXQLXPYBLZBYHXIUOYCXQYIYCBX
      JTZYIXQYCYLBYHTZYIYCYMBYGUNZYHTZYLYCBYNOZYMYOUPYCBYGUQQOYPBYAURBYGUSUTBYN
      YHVAVBBXJYGVCVDYHVEFZYMYIKYHXJXICVFZVGZXJYGVHVIZBYHVEVJVKVLBXJVMVNVOYKXIY
      HHIZYJXIYAYRDVFVPYQUVAYJKYTXIYHVQVKVRVSVTWAVOWBDBXJWCWDXTXJXIHIZJXSXPXTXR
      UVBXRXTUVBXRXTUVBXRXTLZXJXIUKIZXIXJUKIZLUVBUVCUVDUVEXJBXIWEXIXJTZUVEXIWFX
      JVEFUVFUVEKYSXIXJVEVJVKVKWGXJXIWHVRWIWJWKXPUVBXJXIOZXPUVBUVGUPZXJEFZXPUVH
      XPXJXIWLXIWMZWNWOXPXJWPZUVGJZXPUVIUVKUVJXJWQVBUVKXIXJFUVLXJXIWRXIYRWSWTVR
      XAVNVLXBWJVDWJXCVRWIXBVO $.
      $([29-May-98]$)

    $( Corollary of Pigeonhole Principle. $)
    php2 $p |- ( ( A e. om /\ B (. A ) -> B ~< A ) $=
      ( vx com wcel wpss csdm wbr cv wa wi wceq eleq1 psseq2 bi2and breq2
      bi2imd cdom cen wn wss pssss cvv visset ssdom2g ax-mp syl adantl php
      ensymg con3 jca brsdom sylibr vtoclg anabsi5 ) ADEZBAFZBAGHZCIZDEZBUTFZJZ
      BUTGHZKUQURJZUSKCADUTALZVCVEVDUSVFVAUQVBURUTADMUTABNOUTABGPQVCBUTRHZBUTSH
      ZTZJVDVCVGVIVBVGVAVBBUTUAZVGBUTUBUTUCEZVJVGKCUDZBUTUCUEUFUGUHVCUTBSHZTVIU
      TBUIVHVMVKVHVMKVLBUTUJUFUKUGULBUTUMUNUOUP $.
      $([31-May-98]$)
  $}

  ${
    $d x A y z f $.  $d y z f B $.  $( ` y z w f ` are dummy. $)
    $( Corollary of Pigeonhole Principle.   Stronger version of Corollary 6C
       of Enderton, p. 135. $)
    php3 $p |- ( ( E. x e. om A ~~ x /\ B (. A ) -> B ~< A ) $=
      ( vz vf vy cv cen wbr com wrex wpss csdm wi breq2 rcbvexv wcel wa cdom
      wn cvv wss ssdom2g imp relen elbrexi pssss syl2an adantall entr exp
      visset ensymg ax-mp syl6 comm adantl adantr wf1o wex cima cres wf1
      f1ores wfo df-f1o pm3.26bd resexg f1oeq1 cla4ev wb imaexg breng sylibr
      3syl php wceq cdm crn imadmrn a1i wfn ccnv f1o4 fndm imaeq2 pm3.27bd
      forn syl 3eqtr3d psseq2 imass2 cdif c0 cfv wfun fnfun sseq2 difss mpbiri
      jca funfvima f1o3 imadif eleq2d sylibd noel2 eldif syl5ibr 19.23adv
      pssnel sylan5 ssdif0 bineg dfpss3 mpbid mtod exp32 syl5ib imp31 brsdom
      exp31 r19.23aiv sylbi ) BAGZHIZAJKZCBLZCBMIZYGBDGZHIZDJKZYHYINZYFYKADJYEY
      JBHOPYLYHYIYLYHYIYKYMDJYJJQZYKYHYIYNYKRZYHRZCBSIZCBHIZTZRYIYPYQYSYKYHYQYN
      BUAQZCBUBZYQYKYHYTUVAYQCBUAUCUDBYJHUEUFCBUGZUHUIYPYRYJCHIZYOYRUVCNZYHYKUV
      DYNYRYKUVCYRYKCYJHIZUVCYRYKUVECBYJUJUKYJUAQZUVEUVCNDULZCYJUMUNUOUPUQURYNY
      KYHUVCTZYNBYJEGZUSZEUTZYHUVHNZYKYNUVJUVLEYNUVJYHUVHYNUVJYHRZRUVCYJUVICVAZ
      HIZUVMUVCUVONZYNUVMCUVNUVICVBZUSZCUVNHIZUVPBYJUVIVCZUVAUVRUVJYHBYJCUVIVDU
      VJUVTBYJUVIVEZBYJUVIVFZVGUVBUHUVRCUVNFGZUSZFUTZUVSUWDUVRFUVQUVIUAQZUVQUAQ
      EULZUVICUAVHUNCUVNUWCUVQVIVJUVNUAQZUVSUWEVKUWFUWHUWGUVICUAVLUNCUVNUAFVMUN
      VNUVCUVSUVOUVCUVSUVOYJCUVNUJUKUPVOUQYNUVNYJLZUVOTUVMYJUVNVPUVMUVNUVIBVAZL
      ZUWIUVJUWKUWIVKZYHUVJUWJYJVQUWLUVJUVIUVIVRZVAZUVIVSZUWJYJUWNUWOVQUVJUVIVT
      WAUVJUVIBWBZUWMBVQZUWNUWJVQUVJUWPUVIWCZYJWBBYJUVIWDVGZBUVIWEZUWMBUVIWFVOU
      VJUWAUWOYJVQUVJUVTUWAUWBWGBYJUVIWHWIWJUWJYJUVNWKWIURUVMUVNUWJUBZUWJUVNUBZ
      TZRUWKUVMUXAUXCYHUXAUVJYHUVAUXAUVBCBUVIWLWIUQUVMUWJUVNWMZWNVQZTZUXCUVJUWC
      BQUWCCQTRZFUTZUXFYHUVJUXHUXFUVJUXGUXFFUVJUWCBCWMZQZUXFUXGUVJUXJUWCUVIWOZU
      XDQZUXFUVJUXJUXKUVIUXIVAZQZUXLUVJUVIWPZUXIUWMUBZRUXJUXNNUVJUXOUXPUVJUWPUX
      OUWSBUVIWQWIUVJUXPUXIBUBZUVJUWPUWQUXPUXQVKUWSUWTUWMBUXIWRVOBCWSWTXAUXIUWC
      UVIXBWIUVJUXMUXDUXKUVJUWRWPZUXMUXDVQUVJUWAUXRBYJUVIXCWGBCUVIXDWIXEXFUXDUX
      KXGUOUWCBCXHXIXJUDFCBXKXLUXBUXEUWJUVNXMXNVNXAUVNUWJXOVNXPXLXQXRXJUVFYKUVK
      VKUVGBYJUAEVMUNXSXTXQXACBYAVNYBYCUDUKYDUD $.
      $([15-Jun-98]$)
  $}

  $( Cardinal ordering agrees with natural number ordering.  Example 3 of
     Enderton, p. 146. $)
  nndomo $p |- ( ( A e. om /\ B e. om ) -> ( A ~<_ B <-> A (_ B ) ) $=
    ( com wcel wa cdom wbr wss wpss wn wi csdm php2 exp pm2.16d domnsym syl5
    adantr word wb ordssnel ordelpss ancoms binegd bitrd nnord syl2an sylibrd
    ssdomg impbid ) ACDZBCDZEZABFGZABHZUMUNBAIZJZUOUKUNUQKULUKBALGZJUQUNUKUPURU
    KUPURABMNOABPQRASZBSZUOUQTUKULUSUTEZUOBADZJUQABUAVAVBUPUTUSVBUPTBAUBUCUDUEA
    UFBUFUGUHUKUOUNKULABCUIRUJ $.
    $([17-Jun-98]$)

  $( Cardinal ordering agrees with natural number ordering. $)
  nnsdomo $p |- ( ( A e. om /\ B e. om ) -> ( A ~< B <-> A (. B ) ) $=
    ( com wcel wa cdom wbr cen wn wss wceq csdm wpss nndomo nneneq binegd
    bi2and brsdom dfpss2 3bitr4g ) ACDBCDEZABFGZABHGZIZEABJZABKZIZEABLGABMUAUBU
    EUDUGABNUAUCUFABOPQABRABST $.
    $([17-Jun-98]$)

  ${
    $d x B $.
    $( A set equinumerous to a proper subset of itself is infinite.  Corollary
       6D(a) of Enderton, p. 136. $)
    pssinf $p |- ( ( A (. B /\ A ~~ B ) -> -. E. x e. om B ~~ x ) $=
      ( wpss cen wbr cv com wrex wn csdm php3 exp comm sdomnen syl6 pm2.03d
      imp ) BCDZBCEFZCAGEFAHIZJSUATSUABCKFZTJUASUBUASUBACBLMNBCOPQR $.
      $([2-Jun-98]$)
  $}

  ${
    $d x y $.  $( ` y ` is dummy. $)
    $( The set of natural numbers is infinite.  Corollary 6D(b) of Enderton,
       p. 136. $)
    ominf $p |- -. E. x e. om om ~~ x $=
      ( vy com cv cen wbr wrex wn wi breq2 rcbvexv wcel wpss pssinf wss wceq
      wa word wb nnord ordom jctir tz7.8 syl biimpd pm2.43i dfpss2 sylibr cvv
      visset ensymg ax-mp syl2an exp r19.23aiv sylbi pm2.01 ) CADZEFZACGZUTHZIV
      AUTCBDZEFZBCGVAUSVCABCURVBCEJKVCVABCVBCLZVCVAVBCMZVBCEFZVAVDVCAVBCNVDVBCO
      VBCPHQZVEVDVGVDVDVGVDVBRZCRZQVDVGSVDVHVIVBTUAUBVBCUCUDUEUFVBCUGUHVBUILVCV
      FIBUJCVBUKULUMUNUOUPUTUQUL $.
      $([2-Jun-98]$)
  $}

  ${
    $d x A $.
    $( Omega strictly dominates a natural number.  Example 3 of Enderton,
       p. 146.  Note that ` A ~<_ om /\ -. om ~~ A ` is true iff ` A ` is
       finite without assuming the axiom of infinity, unlike ` A ~< om ` .
       This is a peculiarity ultimately due to our ordered pair definition. $)
    omsdomnn $p |- ( A e. om -> ( A ~<_ om /\ -. om ~~ A ) ) $=
      ( vx com wcel cdom wbr cen wn wss ssdomg word ordelss ordom mpan mpd cv
      wceq breq2 binegd wral wrex ralnex ominf mpbir rspec vtoclga jca ) ACDZAC
      EFZCAGFZHZUHACIZUIACCJCKUHULCALMNOCBPZGFZHZUKBACUMAQUNUJUMACGRSUOBCUOBCTU
      NBCUAHUNBCUBBUCUDUEUFUG $.
      $([15-Jun-98]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` y z w ` are dummy. $)
    $( A proper subset of a natural number is equinumerous to some smaller
       number.  Lemma 6F of Enderton, p. 137. $)
    pssnn $p |- ( ( A e. om /\ B (. A ) -> E. x e. A B ~~ x ) $=
      ( vw vz vy com wcel wpss cv cen wbr wrex wi cvv wss ssexg pssss syl5 wal
      wceq psseq1 breq1 birexdv bi2imd cla4gv c0 csuc psseq2 rexeq bialdv weq
      npss0 pm2.21i ax-gen ax-17 hba1 wel wa wn eleq1 biimpcd pm2.16d adantl
      ssd elsuci ord pm2.15d syl6 imp syld exp com23 ssrdv imran dfpss2 sylibr
      wex elelsuc 19.22i df-rex 3imtr4 syl34 exp4c a4s com4t csn cdif word
      nnord orddif syl sseq2d ssdif syl5bir eleq2 eldif pm3.26bd syl6bir
      eleq1a sylan9r adantr pm2.61d imp3a 19.23adv pssnel im2anan9r anabs
      3imtr4g cbvalv visset difss ssexi cla4v sylbi sylan9 ordsucel biimpd
      adantrd cun snssi undif3 sylib uncom syl6eq df-suc a1i breq12d cin enun
      cop wf1o f1osn snex f1oen ax-mp jctr orddisj incom cleq1i indif0 mpbi
      jctil syl2an elnn sylan5d exp4d com24 imp4b jcad breq2 rcla4ev rcbvexv
      exp31 suceq eleq2d sucid mpbii enrefg jctir a1d pm2.61ii 19.21ad finds
      pm2.43a pm2.43d ) BGHZCBIZCAJZKLZABMZUWGUWHUWKUWHUWGUWHUWKNZUWGUWHCOHZUWG
      UWLNUWGCBPUWMUWHCBGQCBRSUWMDJZBIZUWNUWIKLZABMZNZDTZUWLUWGUWRUWLDCOUWNCUAZ
      UWOUWHUWQUWKUWNCBUBUWTUWPUWJABUWNCUWIKUCUDUEUFUWNEJZIZUWPAUXAMZNZDTUWNUGI
      ZUWPAUGMZNZDTUWNFJZIZUWPAUXHMZNZDTZUWNUXHUHZIZUWPAUXMMZNZDTUWSEFBUXAUGUAZ
      UXDUXGDUXQUXBUXEUXCUXFUXAUGUWNUIUWPAUXAUGUJUEUKEFULZUXDUXKDUXRUXBUXIUXCUX
      JUXAUXHUWNUIUWPAUXAUXHUJUEUKUXAUXMUAZUXDUXPDUXSUXBUXNUXCUXOUXAUXMUWNUIUWP
      AUXAUXMUJUEUKUXABUAZUXDUWRDUXTUXBUWOUXCUWQUXABUWNUIUWPAUXABUJUEUKUXGDUXEU
      XFUWNUMUNUOUXHGHZUXLUXPDUYADUPUXKDUQUYAUXLUXPFDURZDFULZUYAUXLUSZUXPNUYDUX
      NUYBUTZUYCUTZUXOUXLUXNUYEUYFUXONNNZUYAUXKUYGDUXKUXNUYEUYFUXOUXNUYEUSZUYFU
      SZUXIUXJUXOUYIUWNUXHPZUYFUSUXIUYHUYJUYFUYHEUWNUXHUXNUYEEDURZEFURZNUXNUYKU
      YEUYLUXNUYKUYEUYLNUXNUYKUSUYEUXRUTZUYLUYKUYEUYMNUXNUYKUXRUYBUXRUYKUYBUXAU
      XHUWNVAVBVCVDUXNUYKUYMUYLNZUXNUYKUXAUXMHZUYNUXNUWNUXMUXAUWNUXMRZVEUYOUYLU
      XRUYOUYLUXRUXAUXHVFVGZVHVIVJVKVLVMVJVNVOUWNUXHVPVQAFURZUWPUSZAVRUWIUXMHZU
      WPUSZAVRUXJUXOUYSVUAAUYRUYTUWPUWIUXHVSVOVTUWPAUXHWAUWPAUXMWAWBWCWDWEVDWFU
      YBUYAUXLUXPUYBUYAUXLUXPUYBUYAUSZUXLUSUXNUWNUXHWGZWHZUWIKLZAUXHMZUXOVUBUXN
      VUDUXHIZUXLVUFVUBUXNUXNUSVUDUXHPZVUDUXHUAZUTZUSUXNVUGUYAUXNVUHUYBUXNVUJUY
      AUWNUXMPZVUHUXNUYAVUHVUDUXMVUCWHZPVUKUYAUXHVULVUDUYAUXHWIZUXHVULUAUXHWJZU
      XHWKWLWMUWNUXMVUCWNWOUYPSUYBUYOUYKUTZUSZEVRVUJUXNUYBVUPVUJEUYBUYOVUOVUJUY
      BUYOVUOVUJNUYBUYOUSZVUIUYKVUQVUIUYKVUQVUIUSUYLUYKVUIUYLUYKNVUQVUIUYLUXAVU
      DHZUYKVUDUXHUXAWPVURUYKUXAVUCHUTUXAUWNVUCWQWRWSVDVUQUYLUTZUYKNVUIUYOVUSUX
      RUYBUYKUYQUXHUWNUXAWTXAXBXCVLVCVLXDXEEUWNUXMXFSXGUXNXHVUDUXHVPXIUXLUXAUXH
      IZUXAUWIKLZAUXHMZNZETVUGVUFNZUXKVVCDEDEULZUXIVUTUXJVVBUWNUXAUXHUBVVEUWPVV
      AAUXHUWNUXAUWIKUCUDUEXJVVCVVDEVUDVUDUWNDXKZUWNVUCXLXMUXAVUDUAZVUTVUGVVBVU
      FUXAVUDUXHUBVVGVVAVUEAUXHUXAVUDUWIKUCUDUEXNXOXPVUBVUFUXONUXLVUBUYRVUEUSZA
      VRUWNUXAKLZEUXMMZVUFUXOVUBVVHVVJAVUBVVHUWIUHZUXMHZUWNVVKKLZUSVVJVUBVVHVVL
      VVMVUBUYRVVLVUEUYAUYRVVLNZUYBUYAVUMVVNVUNVUMUYRVVLUWIUXHXQXRWLVDXSUYBUYAU
      YRVUEVVMUYBVUEUYRUYAVVMUYBVUEUYRUYAVVMUYBVUEUWIGHZVVMUYRUYAUSUYBVVMVUDVUC
      XTZUWIUWIWGZXTZKLZVUEVVOUSUYBUWNVVPVVKVVRKUYBUWNVUCVUDXTZVVPUYBVUCUWNPUWN
      VVTUAUXHUWNYAVUCUWNYBYCVUCVUDYDYEVVKVVRUAUYBUWIYFYGYHVUEVUCVVQKLZUSVUDVUC
      YIZUGUAZUWIVVQYIUGUAZUSVVSVUEVVOVUDUWIVUCVVQYJVUEVWAVUCVVQUXHUWIYKWGZYLVW
      AUXHUWIFXKAXKYMVUCVVQVWEUXHYNYOYPYQVVOVWDVWCVVOUWIWIVWDUWIWJUWIYRWLVUCVUD
      YIZUGUAVWCVWFVWBUGVUCVUDYSYTVUCUWNUVAUVBUVCUVDWOUWIUXHUVEUVFUVGUVHUVIUVJV
      VIVVMEVVKUXMUXAVVKUWNKUVKUVLVIXEVUEAUXHWAUWPVVIAEUXMUWIUXAUWNKUVKUVMXIXBV
      KUVNXDUYCUXPUYDUYCUXOUXNUYCUWNUXMHZUWNUWNKLZUSUXOUYCVWGVWHUYCUWNUWNUHZHVW
      GUYCVWIUXMUWNUWNUXHUVOUVPUWNVVFUVQUVRUWNOHVWHVVFUWNOUVSYPUVTUWPVWHAUWNUXM
      UWIUWNUWNKUVKUVLWLUWAUWAUWBVLUWCUWDSVIUWEUWFVJ $.
      $([22-Jun-98]$)

    $( A subset of a natural number is finite. $)
    ssnn $p |- ( ( A e. om /\ B (_ A ) -> E. x e. om B ~~ x ) $=
      ( com wcel wpss wceq wo cv cen wbr wrex wss wa andi wi wex elnn exp comm
      imrand 19.22dv df-rex 3imtr4g adantr pssnn mpd eleq1 biimparc enrefg
      ancli breq2 rcla4ev 3syl jaoi sylbi sspss sylan5b ) BDEZCBFZCBGZHZCAIZJKZ
      ADLZCBMUSVBNUSUTNZUSVANZHVEUSUTVAOVFVEVGVFVDABLZVEUSVHVEPUTUSVCBEZVDNZAQV
      CDEZVDNZAQVHVEUSVJVLAUSVIVKVDVIUSVKVIUSVKVCBRSTUAUBVDABUCVDADUCUDUEABCUFU
      GVGCDEZVMCCJKZNVEVAVMUSCBDUHUIVMVNCDUJUKVDVNACDVCCCJULUMUNUOUPCBUQUR $.
      $([24-Jun-98]$)

    $( A subset of a finite set is finite.  Corollary 6G of Enderton,
       p. 138. $)
    ssfi $p |- ( ( E. x e. om A ~~ x /\ B (_ A ) -> E. x e. om B ~~ x ) $=
      ( vy vz cv cen wbr com wrex wss wa wi wcel wf1o wex breng cima cres entr
      cvv visset resexg ax-mp f1oeq1 cla4ev imaexg bren sylibr sylan wf1
      f1ores wfo df-f1o pm3.26bd exp r19.22dv adantl ssnn pm3.27bd crn forn
      sseq2d imassrn mpbii syl sylan5 adantarr mpd exp32 19.23adv sylbid
      r19.23aiv imp breq2 rcbvexv sylib ) BAFZGHZAIJZCBKZLCDFZGHZDIJZCVRGHZAIJV
      TWAWDVSWAWDMZAIVRINZVSBVREFZOZEPWFBVRIEQWGWIWFEWGWIWAWDWGWIWALZLWHCRZWBGH
      ZDIJZWDWJWMWDMWGWJWLWCDIWJWLWCCWKWHCSZOZWLWCWJCWKGHZWLWCWOCWKWBTWOCWKVROZ
      APWPWQWOAWNWHUANZWNUANEUBZWHCUAUCUDCWKVRWNUEUFCWKAWRWKUANWSWHCUAUGUDUHUIU
      JBVRWHUKZWAWOWIBVRCWHULWIWTBVRWHUMZBVRWHUNZUOUJUJUPUQURWGWIWMWAWGWKVRKZWM
      WIDVRWKUSWIXAXCWIWTXAXBUTXAWKWHVAZKXCXAXDVRWKBVRWHVBVCWHCVDVEVFVGVHVIVJVK
      VLVMVNWCWEDAIWBVRCGVOVPVQ $.
      $([24-Jun-98]$)
  $}


