$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Finite Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for Pigeonhole Principle.  This just says that if we remove an
     element of a set then put it back in, we end up with the original set. $)
  phplem1 $p |- ( B e. A -> ( { B } u. ( A \ { B } ) ) = A ) $=
    ( wcel csn wss cdif cun wceq snssi ssundif sylib ) BACBDZAELALFGAHBAILAJK
    $.
    $( [25-May-98] $)

  $( Lemma for Pigeonhole Principle.  If we join a natural number to itself
     minus an element, we end up with its successor minus the same element. $)
  phplem2 $p |- ( ( A e. om /\ B e. A ) ->
             ( { A } u. ( A \ { B } ) ) = ( suc A \ { B } ) ) $=
    ( com wcel wa csn cin c0 wceq cdif cun csuc word wn nordeq disjsn2 syl
    nnord sylan undif4 df-suc uncom eqtr difeq1i syl6eqr ) ACDZBADZEAFZBFZGHIZU
    HAUIJKZALZUIJZIAMZUGUJUFUNUGEABINUJABOABPQARSUJUKUHAKZUIJUMUHAUITULUOUIULAU
    HKUOAUAAUHUBUCUDUEQ $.
    $( [25-May-98] $)

  ${
    $d f A $.  $d f B $.  $( ` f ` is dummy. $)
    phplem3.1 $e |- A e. V $.
    phplem3.2 $e |- B e. V $.
    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous to its
       successor minus one of its elements. $)
    phplem3 $p |- ( ( A e. om /\ B e. A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wa csn cdif cun csuc cen wbr cin c0 wceq wss difss ssrin
      ax-mp word nnord orddisj syl sseq2d mpbii ss0 incom syl5eq indif0 jctil
      cop wf1o f1osn snex f1oen ssexi enref pm3.2i unen adantr phplem1 adantl
      phplem2 3brtr3d ) AEFZBAFZGBHZAVHIZJZAHZVIJZAAKVHILVFVJVLLMZVGVFVHVKLMZVI
      VILMZGZVHVINOPZVKVINZOPZGZGVMVFVTVPVFVSVQVFVIVKNZOVRVFWAOQZWAOPVFWAAVKNZQ
      ZWBVIAQWDAVHRZVIAVKSTVFWCOWAVFAUAWCOPAUBAUCUDUEUFWAUGUDVKVIUHUIVHAUJUKVNV
      OVHVKBAULHZUMVNBADCUNVHVKWFBUOUPTVIVIACWEUQURUSUKVHVKVIVIUTUDVAVGVJAPVFAB
      VBVCABVDVE $.
      $( [11-Jun-98] $)

    $( Lemma for Pigeonhole Principle.  A natural number is equinumerous
       to its successor minus any element of the successor. $)
    phplem4 $p |- ( ( A e. om /\ B e. suc A ) -> A ~~ ( suc A \ { B } ) ) $=
      ( com wcel wceq wo csuc csn cdif cen wbr wa andi phplem3 word nnord
      orddif syl sneq difeq2d cleqcoms sylan9eq enref syl5breq jaoi sylbi
      elsuci sylan5 ) AEFZBAFZBAGZHZAAIZBJZKZLMZBUOFUKUNNUKULNZUKUMNZHURUKULUMO
      USURUTABCDPUTAUQALUKUMAUOAJZKZUQUKAQAVBGARASTVBUQGABABGVAUPUOABUAUBUCUDAC
      UEUFUGUHBAUIUJ $.
      $( [26-May-98] $)

    $( Lemma for Pigeonhole Principle.  Equinumerosity of successors implies
       equinumerosity of the original natural numbers. $)
    phplem5 $p |- ( ( A e. om /\ B e. om ) -> ( suc A ~~ suc B -> A ~~ B ) ) $=
      ( vf com wcel wa csuc cv wf1o wex cen wbr cfv csn cdif entrt cima wf1
      wss cres f1of1 sssucid jctir f1ores f1oen 3syl adantl word wceq nnord
      orddif imaeq2 wfn f1ofn sucid fnsnfv mpan2 difeq2 crn cdm imadmrn
      cleqcomi a1i wfo f1ofo forn syl fndm 3eqtr3d difeq1d ccnv wfun f1o3
      pm3.27bd imadif 3eqtr4rd sylan9eq breqtrd fvex phplem4 fnfvrn wb eleq2d
      mpbid sylan5 sucex difss ssexi ener ersym syl2an anandirs exp 19.23adv
      bren syl5ib ) AFGZBFGZHZAIZBIZEJZKZELABMNZXBXCMNXAXEXFEXAXEXFWSWTXEXFAXCA
      XDOZPZQZMNXIBMNZXFWSXEHZWTXEHZAXIBRXKAXDASZXIMXEAXMMNZWSXEXBXCXDTZAXBUAZH
      AXMXDAUBZKXNXEXOXPXBXCXDUCAUDUEXBXCAXDUFAXMXQCUGUHUIWSXEXMXDXBAPZQZSZXIWS
      AUJAXSUKXMXTUKAULAUMAXSXDUNUHXEXDXBSZXHQZYAXDXRSZQZXIXTXEXDXBUOZXHYCUKZYB
      YDUKXBXCXDUPZYEAXBGZYFACUQZXBAXDURUSXHYCYAUTUHXEXCYAXHXEXDVAZXDXDVBZSZXCY
      AYJYLUKXEYLYJXDVCVDVEXEXBXCXDVFZYJXCUKXBXCXDVGZXBXCXDVHZVIXEYEYKXBUKYLYAU
      KYGXBXDVJYKXBXDUNUHVKVLXEXDVMVNZXTYDUKXEYMYPXBXCXDVOVPXBXRXDVQVIVRVSVTXLB
      XIMNZXJWTXGXCGZYQXEBXGDAXDWAWBXEXGYJGZYRXEYEYSYGYEYHYSYIXBAXDWCUSVIXEYMYS
      YRWDYNYMYJXCXGYOWEVIWFWGBXIMDXIXCBDWHZXCXHWIWJWKWLVIWMWNWOWPXBXCEYTWQWR
      $.
      $( [28-May-98] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Two equinumerous natural numbers are equal.  Proposition 10.20 of
       [TakeutiZaring] p. 90 and its converse.  Also compare Corollary 6E of
       [Enderton] p. 136. $)
    nneneq $p |- ( ( A e. om /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
      ( vz vx vy vw com wcel wa cen wbr wceq wi cv wral weq c0 csuc breq1
      cleq1 bi2imd biraldv 0ex visset ener ersym en0 cleqcom bitr sylib a1i
      rgen wrex wo wb breq2 cleq2 bi2bid mpbiri biimpd ax-17 hbra1 hban
      phplem5 syl4d exp a2d ra4 syl5 imp suceq syl8 biimprcd syl6 r19.23ad
      jaod nn0suc syl7 r19.21adv cbvralv syl6ib finds rcla4v syl eqeng adantr
      impbid ) AGHZBGHZIABJKZABLZWHWIWJWKMZWHACNZJKZAWMLZMZCGOZWIWLMDNZWMJKZDCP
      ZMZCGOQWMJKZQWMLZMZCGOENZWMJKZECPZMZCGOZXERZWMJKZXJWMLZMZCGOZWQDEAWRQLZXA
      XDCGXOWSXBWTXCWRQWMJSWRQWMTUAUBDEPZXAXHCGXPWSXFWTXGWRXEWMJSWRXEWMTUAUBWRX
      JLZXAXMCGXQWSXKWTXLWRXJWMJSWRXJWMTUAUBWRALZXAWPCGXRWSWNWTWOWRAWMJSWRAWMTU
      AUBXDCGXDWMGHZXBWMQJKZXCQWMJUCCUDZUEUFXTWMQLXCWMUGWMQUHUIUJUKULXEGHZXIXJF
      NZJKZXJYCLZMZFGOXNYBXIYFFGYBXIYCQLZYCWMRZLZCGUMZUNZYFYCGHYBXIYKYFMYBXIIZY
      GYFYJYGYFMYLYGYDYEYGYDYEUOXJQJKZXJQLZUOXJUGYGYDYMYEYNYCQXJJUPYCQXJUQURUSU
      TUKYLYIYFCGYBXICYBCVAXHCGVBVCYFCVAYLXSXJYHJKZXJYHLZMZYIYFMYLXSYOXGYPYBXIX
      SYOXGMZMZYBXSXHMYSXIYBXSXHYRYBXSXHYRMYBXSIYOXFXGXEWMEUDYAVDVEVFVGXHCGVHVI
      VJXEWMVKVLYIYFYQYIYDYOYEYPYCYHXJJUPYCYHXJUQUAVMVNVOVPVFCYCVQVRVSYFXMFCGFC
      PYDXKYEXLYCWMXJJUPYCWMXJUQUAVTWAWBWPWLCBGWMBLWNWJWOWKWMBAJUPWMBAUQUAWCWDV
      JWHWKWJMWIABGWEWFWG $.
      $( [28-May-98] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Pigeonhole Principle.  A natural number is not equinumerous to a
       proper subset of itself.  Theorem (Pigeonhole Principle) of [Enderton]
       p. 134.  The theorem is so-called because you can't put _n_ + 1
       pigeons into _n_ holes (if each hole holds only one pigeon).
       The proof consists of lemmas ~ phplem1 through ~ phplem5 , ~ nneneq ,
       and this final piece of the proof. $)
    php $p |- ( ( A e. om /\ B (. A ) -> -. A ~~ B ) $=
      ( vx vy com wcel wpss cen wbr wn wi wa cv csuc wceq wrex c0 nn0suc
      orcanai wss 0ss sspsstr mpan 0pss sylib sylan5 psseq2 breq1 binegd
      bi2imd cdom wex pssnel csn cdif domentr wb cin disjsn disj3 bitr3 sseq1
      sylbi ssdif syl5bir cvv visset sucex difss ssexi ssdom2g ax-mp syl6
      pssss syl5 imp phplem4 ensym syl syl2an exp43 com4r 19.23aiv mpcom
      endomtr sssucid mp2 jctir sbth exp com12 word peano2b nnord sucid nordeq
      mpan2 nneneq sylanb anidms mtbird nsyli pm2.43d r19.23aiv ) AEFZBAGZABHIZ
      JZXEXFXHXEXFXFXHKZXEXFLACMZNZOZCEPZXIXEAQOZJZXMXFXEXNXMCARSXFQAGZXOQBTXFX
      PBUAQBAUBUCAUDUEUFXLXICEXLXJEFZXIXLXIBXKGZXKBHIZJZKXQXLXFXRXHXTAXKBUGXLXG
      XSAXKBHUHUIUJXRXQXTXRXQXTXRXQBXJUKIZXQXTKDMZXKFZYBBFJZLZDULXRXQYAKZDBXKUM
      YEXRYFKZDYCYDYGYDXRXQYCYAYDXRXQYCYABXKYBUNZUOZUKIZYIXJHIZYAYDXRLXQYCLZBYI
      XJUPYDXRYJYDBXKTZYJXRYDYMBYITZYJYDYNBYHUOZYITZYMYDBYOOZYNYPUQYDBYHURQOYQB
      YBUSBYHUTVABYOYIVBVCBXKYHVDVEYIVFFYNYJKYIXKXJCVGZVHZXKYHVIVJZBYIVFVKVLVMB
      XKVNVOVPYLXJYIHIYKXJYBYRDVGVQXJYIYTVRVSVTWAWBVPWCWDYAXSXKXJHIZXQXSYAUVAXS
      YAUVAXSYALZXKXJUKIZXJXKUKIZLUVAUVBUVCUVDXKBXJWEXKVFFXJXKTUVDYSXJWFXJXKVFV
      KWGWHXKXJWIVSWJWKXQUVAXKXJOZXQXKWLZUVEJZXQXKEFZUVFXJWMZXKWNVCUVFXJXKFUVGX
      JYRWOXKXJWPWQVSXQUVAUVEUQZUVHXQUVJXQXKXJWRUVIWSWTXAXBVMXCWKVEWKXDVSWJXCVP
      $.
      $( [29-May-98] $)

    $( Corollary of Pigeonhole Principle. $)
    php2 $p |- ( ( A e. om /\ B (. A ) -> B ~< A ) $=
      ( vx com wcel wpss csdm wbr cv wa wi wceq eleq1 psseq2 bi2and breq2
      bi2imd cdom cen wn wss pssss cvv visset ssdom2g ax-mp syl adantl php
      ensym nsyl jca brsdom sylibr vtoclg anabsi5 ) ADEZBAFZBAGHZCIZDEZBUTFZJZB
      UTGHZKUQURJZUSKCADUTALZVCVEVDUSVFVAUQVBURUTADMUTABNOUTABGPQVCBUTRHZBUTSHZ
      TZJVDVCVGVIVBVGVAVBBUTUAZVGBUTUBUTUCEVJVGKCUDZBUTUCUEUFUGUHVCUTBSHVHUTBUI
      BUTVKUJUKULBUTUMUNUOUP $.
      $( [31-May-98] $)
  $}

  ${
    $d x A y z f $.  $d y z f B $.  $( ` y z w f ` are dummy. $)
    $( Corollary of Pigeonhole Principle.   If ` A ` is finite and ` B `
       is a proper subset of ` A ` , the ` B ` is strictly less numerous
       than ` A ` .  Stronger version of Corollary 6C of [Enderton] p. 135.
       (The expression ` E. x e. om A ~~ x ` is the definition
       of "finite", and "infinite" is defined as "not finite".) $)
    php3 $p |- ( ( E. x e. om A ~~ x /\ B (. A ) -> B ~< A ) $=
      ( vz vf vy cv cen wbr com wrex wpss csdm wi wcel wa cdom wn cvv wss
      ssdom2g imp relen brrelexi pssss syl2an adantll wf1o wex cima php imass2
      syl adantl cdif c0 wceq cfv wfun cdm funfvima2 f1ofun difss wfn wb f1ofn
      fndm sseq2 3syl mpbiri sylan13 ccnv wfo f1o3 pm3.27bd imadif eleq2d
      sylibd noel2 syl6 eldif syl5ibr 19.23adv pssnel sylan5 ssdif0 bineg
      sylibr jca dfpss3 crn imadmrn a1i imaeq2 f1ofo forn 3eqtr3d psseq2d
      adantr mpbid cres wf1 f1ores f1of1 visset resexg ax-mp f1oeq1 cla4ev
      imaexg bren entrt exp com12 mtod exp32 syl5ib imp31 ensym ad2antlr
      brsdom exp31 r19.23aiv breq2 cbvrexv sylanb ) BDGZHIZDJKZCBLZCBMIZBAGZHIZ
      AJKYIYJYKYHYJYKNDJYGJOZYHYJYKYNYHPYJPZCBQIZCBHIZRZPYKYOYPYRYHYJYPYNBSOZCB
      TZYPYHYJYSYTYPCBSUAUBBYGHUCUDCBUEZUFUGYOYQYGCHIZYNYHYJUVBRZYNBYGEGZUHZEUI
      YJUVCNZYHYNUVEUVFEYNUVEYJUVCYNUVEYJPZPUVBYGUVDCUJZHIZYNUVHYGLZUVIRUVGYGUV
      HUKUVGUVHUVDBUJZLZUVJUVGUVHUVKTZUVKUVHTZRZPUVLUVGUVMUVOYJUVMUVEYJYTUVMUVA
      CBUVDULUMUNUVGUVKUVHUOZUPUQZRZUVOUVEFGZBOUVSCORPZFUIZUVRYJUVEUWAUVRUVEUVT
      UVRFUVEUVSBCUOZOZUVRUVTUVEUWCUVSUVDURZUVPOZUVRUVEUWCUWDUVDUWBUJZOZUWEUVDU
      SUWBUVDUTZTZUWCUWGNUVEUWBUVSUVDVABYGUVDVBUVEUWIUWBBTZBCVCUVEUVDBVDZUWHBUQ
      ZUWIUWJVEBYGUVDVFZBUVDVGZUWHBUWBVHVIVJVKUVEUWFUVPUWDUVEUVDVLUSZUWFUVPUQUV
      EBYGUVDVMZUWOBYGUVDVNVOBCUVDVPUMVQVRUVPUWDVSVTUVSBCWAWBWCUBFCBWDWEUVNUVQU
      VKUVHWFWGWHWIUVHUVKWJWHUVEUVLUVJVEYJUVEUVKYGUVHUVEUVDUWHUJZUVDWKZUVKYGUWQ
      UWRUQUVEUVDWLWMUVEUWKUWLUWQUVKUQUWMUWNUWHBUVDWNVIUVEUWPUWRYGUQBYGUVDWOBYG
      UVDWPUMWQWRWSWTWEUVGUVBUVINZYNUVGCUVHUVDCXAZUHZCUVHHIZUWSBYGUVDXBYTUXAUVE
      YJBYGCUVDXCBYGUVDXDUVAUFUXACUVHUVSUHZFUIUXBUXCUXAFUWTUVDSOZUWTSOEXEZUVDCS
      XFXGCUVHUVSUWTXHXICUVHFUXDUVHSOUXEUVDCSXJXGXKWHUVBUXBUVIUVBUXBUVIYGCUVHXL
      XMXNVIUNXOXPWCBYGEDXEZXKXQXRYHYQUVBNYNYJYQYHUVBYQYHCYGHIZUVBYQYHUXGCBYGXL
      XMCYGUXFXSVTXNXTXOWICBYAWHYBYCUBYMYHADJYLYGBHYDYEYF $.
      $( [15-Jun-98] $)
  $}

  $( Corollary of the Pigeonhole Principle ~ php :  a natural number is
     strictly dominated by its successor. $)
  php4 $p |- ( A e. om -> A ~< suc A ) $=
    ( com wcel csuc wpss csdm wbr sucidg word wa wb nnord ordsuc biimp ancli
    ordelpss 3syl mpbid php2 peano2b sylanb mpand ) ABCZAADZEZAUDFGZUCAUDCZUEAB
    HUCAIZUHUDIZJUGUEKALUHUIUHUIAMNOAUDPQRUDBCUEUFUCUDASATUAUB $.
    $( [3-Sep-04] $) $( [26-Jul-04] $)

  $( Corollary of the Pigeonhole Principle ~ php :  a natural number is not
     equinumerous to its successor.  Corollary 10.21(1) of [TakeutiZaring]
     p. 90. $)
  php5 $p |- ( A e. om -> -. A ~~ suc A ) $=
    ( com wcel csuc csdm wbr cen wn php4 sdomnen syl ) ABCAADZEFALGFHAIALJK $.
    $( [8-Sep-04] $) $( [26-Jul-04] $)

  $( An ordinal number equinumerous to a natural number is equal to it.
     Proposition 10.22 of [TakeutiZaring] p. 90 and its converse. $)
  onomeneq $p |- ( ( A e. On /\ B e. om ) -> ( A ~~ B <-> A = B ) ) $=
    ( con0 wcel com wa cen wbr wceq nneneq biimpa wss wn csuc php5 adantr
    enen1 mtbird adantll cdom wi endomtr sssucid ssdomg mpi sylan5 ancoms a1d
    ssel com12 word wb ordelsuc eloni sylibd ssdom2g adantl syld jcad sbth
    syl6 mtod ordom ordtri1 mpan syl bicon2d ad2antll mpbird pm3.27 jca
    sylan13 exp eqeng impbid ) ACDZBEDZFZABGHZABIZVRVSVTAEDZVQFZVSVTVRVSFZWBVSV
    TABJKWCWAVQWCWAEALZMZWCWDABNZGHZVQVSWGMVPVQVSFZWGBWFGHZVQWIMVSBOPABWFEQRSWC
    WDAWFTHZWFATHZFWGWCWDWJWKVQVSWDWJUAVPWHWJWDVSVQWJVSBWFTHZWJVQABWFUBVQBWFLWL
    BUCBWFEUDUEUFUGUHSVRWDWKUAZVSVQVPWMVQVPFZWDWFALZWKWNWDBADZWOVQWDWPUAVPWDVQW
    PEABUIUJPVQAUKZWPWOULVPBAEUMAUNZUFUOVPWOWKUAVQWFACUPUQURUGPUSAWFUTVAVBVPWAW
    EULVQVSVPWDWAVPWQWDWAMULZWREUKWQWSVCEAVDVEVFVGVHVIVRVQVSVPVQVJPVKVRVSVJVLVM
    VPVTVSUAVQABCVNPVO $.
    $( [?] $) $( [26-Jul-04] $)

  ${
    $d x A $.
    $( An ordinal number is finite iff it is a natural number.  Proposition 10.32
       of [TakeutiZaring] p. 92. $)
    onfin $p |- ( A e. On -> ( E. x e. om A ~~ x <-> A e. om ) ) $=
      ( con0 wcel cv cen wbr com wrex wi wa wceq onomeneq eleq1a adantl sylbid
      exp r19.23adv enrefg breq2 rcla4ev mpand a1i impbid ) BCDZBAEZFGZAHIZBHDZ
      UEUGUIAHUEUFHDZUGUIJUEUJKUGBUFLZUIBUFMUJUKUIJUEUFHBNOPQRUIUHJUEUIBBFGZUHB
      HSUGULABHUFBBFTUAUBUCUD $.
      $( [?] $) $( [26-Jul-04] $)
  $}

  $( Cardinal ordering agrees with natural number ordering.  Example 3 of
     [Enderton] p. 146. $)
  nndomo $p |- ( ( A e. om /\ B e. om ) -> ( A ~<_ B <-> A (_ B ) ) $=
    ( com wcel wa cdom wbr wss wpss wn wi csdm php2 exp domnsym nsyli adantr
    word wb ordtri1 ordelpss ancoms binegd bitrd nnord syl2an sylibrd ssdomg
    impbid ) ACDZBCDZEZABFGZABHZULUMBAIZJZUNUJUMUPKUKUJUOBALGZUMUJUOUQABMNABOPQ
    ARZBRZUNUPSUJUKURUSEZUNBADZJUPABTUTVAUOUSURVAUOSBAUAUBUCUDAUEBUEUFUGUJUNUMK
    UKABCUHQUI $.
    $( [17-Jun-98] $)

  $( Cardinal ordering agrees with natural number ordering. $)
  nnsdomo $p |- ( ( A e. om /\ B e. om ) -> ( A ~< B <-> A (. B ) ) $=
    ( com wcel wa cdom wbr cen wn wss wceq csdm wpss nndomo nneneq binegd
    bi2and brsdom dfpss2 3bitr4g ) ACDBCDEZABFGZABHGZIZEABJZABKZIZEABLGABMUAUBU
    EUDUGABNUAUCUFABOPQABRABST $.
    $( [17-Jun-98] $)

  $( Strict dominance of natural numbers is the same as dominance over the
     successor of the smaller. $)
  omomsdomdom $p |- ( ( A e. om /\ B e. om ) -> ( A ~< B <-> suc A ~<_ B ) ) $=
    ( com wcel wa wpss csuc wss csdm wbr cdom word wb ordelpss nnord sylan
    ordelsuc bitr3d sylan5 nnsdomo nndomo peano2b sylanb 3bitr4d ) ACDZBCDZEABF
    ZAGZBHZABIJUHBKJZUEBLZUGUIMUFUEUKEABDZUGUIALUKULUGMUEABNAOPABCQRBOSABTUHCDU
    FUJUIMUEUHBUAAUBUCUD $.
    $( [?] $) $( [25-Jul-04] $)

  $( Dominance of a set over a successor of a natural number implies strict
     dominance over the number.   For the converse, see ~ omsdomdom . $)
  omdomsdom $p |- ( ( A e. om /\ B e. C ) ->
                   ( suc A ~<_ B -> A ~< B ) ) $=
    ( csuc csdm wbr wcel cdom wi com sdomdomtr exp3a com12 imp php4 sylan ) AAD
    ZEFZBCGZQBHFZABEFZIZAJGRSUBSRUBSRTUAAQBCKLMNAOP $.
    $( [?] $) $( [26-Jul-04] $)

  ${
    $d x A $.  $d x B $.
    $( Strict dominance of a finite set over a natural number is the same as
       dominance over its successor. $)
    finsdomdom $p |- ( ( A e. om /\ E. x e. om B ~~ x ) ->
                     ( A ~< B <-> suc A ~<_ B ) ) $=
      ( com wcel cv cen wbr wrex csdm csuc cdom wb wa omomsdomdom adantr
      sdomen2 adantll domen2 3bitr4d exp31 r19.23adv imp ) BDEZCAFZGHZADIBCJHZB
      KZCLHZMZUDUFUJADUDUEDEZUFUJUDUKNZUFNBUEJHZUHUELHZUGUIULUMUNMUFBUEOPUKUFUG
      UMMUDCUEBDQRUKUFUIUNMUDCUEUHDSRTUAUBUC $.
      $( [?] $) $( [26-Jul-04] $)
  $}

  ${
    $d x B $.
    $( A set equinumerous to a proper subset of itself is infinite.  Corollary
       6D(a) of [Enderton] p. 136. $)
    pssinf $p |- ( ( A (. B /\ A ~~ B ) -> -. E. x e. om B ~~ x ) $=
      ( wpss cen wbr cv com wrex wn csdm php3 exp com12 sdomnen syl6 con2d imp
      ) BCDZBCEFZCAGEFAHIZJSUATSUABCKFZTJUASUBUASUBACBLMNBCOPQR $.
      $( [2-Jun-98] $)
  $}

  ${
    $d x y $.  $( ` y ` is dummy. $)
    $( The set of natural numbers is infinite.  Corollary 6D(b) of [Enderton]
       p. 136. $)
    ominf $p |- -. E. x e. om om ~~ x $=
      ( vy com cv cen wbr wrex wn wi breq2 cbvrexv wcel wpss pssinf wss wceq
      wa word wb nnord ordom jctir ordelssne syl ibi dfpss2 sylibr visset
      ensym syl2an exp r19.23aiv sylbi pm2.01 ax-mp ) CADZEFZACGZURHZIUSURCBDZE
      FZBCGUSUQVAABCUPUTCEJKVAUSBCUTCLZVAUSUTCMZUTCEFUSVBVAAUTCNVBUTCOUTCPHQZVC
      VBVDVBUTRZCRZQVBVDSVBVEVFUTTUAUBUTCUCUDUEUTCUFUGCUTBUHUIUJUKULUMURUNUO $.
      $( [2-Jun-98] $)
  $}

  ${
    $d x A $.
    $( Omega strictly dominates a natural number.  Example 3 of [Enderton]
       p. 146.  Here we use ` A ~<_ om /\ -. om ~~ A ` instead of ` A ~< om `
       because, due to a peculiarity ultimately caused our ordered pair
       definition, we would need the axiom of infinity (which we have avoided
       up to now) in order to prove the latter. $)
    omsdomnn $p |- ( A e. om -> ( A ~<_ om /\ -. om ~~ A ) ) $=
      ( vx com wcel cdom wbr cen wn wss word ordom ordelss mpan ssdomg mpd cv
      wceq breq2 binegd wral wrex ominf ralnex mpbir vtoclri jca ) ACDZACEFZCAG
      FZHZUGACIZUHCJUGUKKCALMACCNOCBPZGFZHZUJBACULAQUMUIULACGRSUNBCTUMBCUAHBUBU
      MBCUCUDUEUF $.
      $( [15-Jun-98] $)

    $( Omega strictly dominates a finite set.  See comment in ~ omsdomnn . $)
    isfinite1 $p |- ( E. x e. om A ~~ x -> ( A ~<_ om /\ -. om ~~ A ) ) $=
      ( cv cen wbr com cdom wn wa wcel omsdomnn imlan anandi sylib ancoms
      endomtr entrt exp com12 con3d imp im2an syl r19.23aiv ) BACZDEZBFGEZFBDEZ
      HZIZAFUEFJZUFUJUKUFIUFUEFGEZIZUFFUEDEZHZIZIZUJUFUKUQUFUKIUFULUOIZIUQUKURU
      FUEKLUFULUOMNOUMUGUPUIBUEFPUFUOUIUFUHUNUHUFUNUHUFUNFBUEQRSTUAUBUCRUD $.
      $( [3-Nov-02] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` y z w ` are dummy. $)
    $( A proper subset of a natural number is equinumerous to some smaller
       number.  Lemma 6F of [Enderton] p. 137. $)
    pssnn $p |- ( ( A e. om /\ B (. A ) -> E. x e. A B ~~ x ) $=
      ( vw vz vy com wcel wpss cv cen wbr wrex wi cvv wss ssexg pssss syl5 wal
      wceq psseq1 breq1 birexdv bi2imd cla4gv c0 csuc psseq2 rexeq bialdv weq
      npss0 pm2.21i ax-gen ax-17 hba1 wel wa wn eleq1 biimpcd con3d adantl
      sseld elsuci ord con1d syl6 imp syld exp com23 ssrdv imran dfpss2 sylibr
      wex elelsuc 19.22i df-rex 3imtr4 syl34 exp4c a4s com4t csn cdif word
      nnord orddif syl sseq2d ssdif syl5bir eleq2 eldifi syl6bir eleq1a
      sylan9r adantr pm2.61d imp3a 19.23adv pssnel im2anan9r anidm syl5ibr
      syl6ibr cbvalv visset difss ssexi cla4v sylbi sylan9 ordsucelsuc biimpd
      adantrd cun snssi ssundif sylib uncom syl5reqr df-suc a1i breq12d cin
      unen cop wf1o f1osn snex f1oen ax-mp jctr orddisj incom indif0 eqtr3
      jctil syl2an elnn sylan5d exp4d com24 imp4b jcad breq2 rcla4ev cbvrexv
      3imtr4g exp31 eqelsuc enref jctir a1d pm2.61ii 19.21ad finds pm2.43a
      pm2.43d ) BGHZCBIZCAJZKLZABMZUWDUWEUWHUWEUWDUWEUWHNZUWDUWECOHZUWDUWINUWDC
      BPUWJUWECBGQCBRSUWJDJZBIZUWKUWFKLZABMZNZDTZUWIUWDUWOUWIDCOUWKCUAZUWLUWEUW
      NUWHUWKCBUBUWQUWMUWGABUWKCUWFKUCUDUEUFUWKEJZIZUWMAUWRMZNZDTUWKUGIZUWMAUGM
      ZNZDTUWKFJZIZUWMAUXEMZNZDTZUWKUXEUHZIZUWMAUXJMZNZDTUWPEFBUWRUGUAZUXAUXDDU
      XNUWSUXBUWTUXCUWRUGUWKUIUWMAUWRUGUJUEUKEFULZUXAUXHDUXOUWSUXFUWTUXGUWRUXEU
      WKUIUWMAUWRUXEUJUEUKUWRUXJUAZUXAUXMDUXPUWSUXKUWTUXLUWRUXJUWKUIUWMAUWRUXJU
      JUEUKUWRBUAZUXAUWODUXQUWSUWLUWTUWNUWRBUWKUIUWMAUWRBUJUEUKUXDDUXBUXCUWKUMU
      NUOUXEGHZUXIUXMDUXRDUPUXHDUQUXRUXIUXMFDURZDFULZUXRUXIUSZUXMNUYAUXKUXSUTZU
      XTUTZUXLUXIUXKUYBUYCUXLNNNZUXRUXHUYDDUXHUXKUYBUYCUXLUXKUYBUSZUYCUSZUXFUXG
      UXLUYFUWKUXEPZUYCUSUXFUYEUYGUYCUYEEUWKUXEUXKUYBEDURZEFURZNUXKUYHUYBUYIUXK
      UYHUYBUYINUXKUYHUSUYBUXOUTZUYIUYHUYBUYJNUXKUYHUXOUXSUXOUYHUXSUWRUXEUWKVAV
      BVCVDUXKUYHUYJUYINZUXKUYHUWRUXJHZUYKUXKUWKUXJUWRUWKUXJRZVEUYLUYIUXOUYLUYI
      UXOUWRUXEVFVGZVHVIVJVKVLVMVJVNVOUWKUXEVPVQAFURZUWMUSZAVRUWFUXJHZUWMUSZAVR
      UXGUXLUYPUYRAUYOUYQUWMUWFUXEVSVOVTUWMAUXEWAUWMAUXJWAWBWCWDWEVDWFUXSUXRUXI
      UXMUXSUXRUXIUXMUXSUXRUSZUXIUSUXKUWKUXEWGZWHZUWFKLZAUXEMZUXLUYSUXKVUAUXEIZ
      UXIVUCUYSUXKVUAUXEPZVUAUXEUAZUTZUSZVUDUYSUXKUXKUSVUHUXKUXRUXKVUEUXSUXKVUG
      UXRUWKUXJPZVUEUXKUXRVUEVUAUXJUYTWHZPVUIUXRUXEVUJVUAUXRUXEWIZUXEVUJUAUXEWJ
      ZUXEWKWLWMUWKUXJUYTWNWOUYMSUXSUYLUYHUTZUSZEVRVUGUXKUXSVUNVUGEUXSUYLVUMVUG
      UXSUYLVUMVUGNUXSUYLUSZVUFUYHVUOVUFUYHVUOVUFUSUYIUYHVUFUYIUYHNVUOVUFUYIUWR
      VUAHUYHVUAUXEUWRWPUWRUWKUYTWQWRVDVUOUYIUTZUYHNVUFUYLVUPUXOUXSUYHUYNUXEUWK
      UWRWSWTXAXBVLVCVLXCXDEUWKUXJXESXFUXKXGXHVUAUXEVPXIUXIUWRUXEIZUWRUWFKLZAUX
      EMZNZETVUDVUCNZUXHVUTDEDEULZUXFVUQUXGVUSUWKUWRUXEUBVVBUWMVURAUXEUWKUWRUWF
      KUCUDUEXJVUTVVAEVUAVUAUWKDXKZUWKUYTXLXMUWRVUAUAZVUQVUDVUSVUCUWRVUAUXEUBVV
      DVURVUBAUXEUWRVUAUWFKUCUDUEXNXOXPUYSVUCUXLNUXIUYSUYOVUBUSZAVRUWKUWRKLZEUX
      JMZVUCUXLUYSVVEVVGAUYSVVEUWFUHZUXJHZUWKVVHKLZUSVVGUYSVVEVVIVVJUYSUYOVVIVU
      BUXRUYOVVINZUXSUXRVUKVVKVULVUKUYOVVIUWFUXEXQXRWLVDXSUXSUXRUYOVUBVVJUXSVUB
      UYOUXRVVJUXSVUBUYOUXRVVJUXSVUBUWFGHZVVJUYOUXRUSUXSVVJVUAUYTXTZUWFUWFWGZXT
      ZKLZVUBVVLUSUXSUWKVVMVVHVVOKUXSUYTVUAXTZUWKVVMUXSUYTUWKPVVQUWKUAUXEUWKYAU
      YTUWKYBYCUYTVUAYDYEVVHVVOUAUXSUWFYFYGYHVUBUYTVVNKLZUSVUAUYTYIZUGUAZUWFVVN
      YIUGUAZUSVVPVUBVVLVUAUWFUYTVVNYJVUBVVRUYTVVNUXEUWFYKWGZYLVVRUXEUWFFXKAXKY
      MUYTVVNVWBUXEYNYOYPYQVVLVWAVVTVVLUWFWIVWAUWFWJUWFYRWLUYTVUAYIVVSUGUYTVUAY
      SUYTUWKYTUVAUVBUVCWOUWFUXEUVDUVEUVFUVGUVHUVIVVFVVJEVVHUXJUWRVVHUWKKUVJUVK
      VIXDVUBAUXEWAUWMVVFAEUXJUWFUWRUWKKUVJUVLUVMXAVKUVNXCUXTUXMUYAUXTUXLUXKUXT
      UWKUXJHZUWKUWKKLZUSUXLUXTVWCVWDUWKUXEVVCUVOUWKVVCUVPUVQUWMVWDAUWKUXJUWFUW
      KUWKKUVJUVKWLUVRUVRUVSVLUVTUWASVIUWBUWCVJ $.
      $( [22-Jun-98] $)

    $( A subset of a natural number is finite. $)
    ssnn $p |- ( ( A e. om /\ B (_ A ) -> E. x e. om B ~~ x ) $=
      ( com wcel wpss wceq wo cv cen wbr wrex wss wa andi pssnn wi wex elnn
      exp com12 imrand 19.22dv df-rex 3imtr4g adantr mpd eleq1 biimparc enrefg
      ancli breq2 rcla4ev 3syl jaoi sylbi sspss sylan5b ) BDEZCBFZCBGZHZCAIZJKZ
      ADLZCBMUSVBNUSUTNZUSVANZHVEUSUTVAOVFVEVGVFVDABLZVEABCPUSVHVEQUTUSVCBEZVDN
      ZARVCDEZVDNZARVHVEUSVJVLAUSVIVKVDVIUSVKVIUSVKVCBSTUAUBUCVDABUDVDADUDUEUFU
      GVGCDEZVMCCJKZNVEVAVMUSCBDUHUIVMVNCDUJUKVDVNACDVCCCJULUMUNUOUPCBUQUR $.
      $( [24-Jun-98] $)

    $( A subset of a finite set is finite.  Corollary 6G of [Enderton]
       p. 138. $)
    ssfi $p |- ( ( E. x e. om A ~~ x /\ B (_ A ) -> E. x e. om B ~~ x ) $=
      ( vy vz cv cen wbr com wrex wss wa wi wcel wf1o wex breng cima ssnn wfo
      wf1 df-f1o pm3.27bd crn imassrn forn sseq2d mpbii syl sylan5 adantrr
      cres entrt cvv visset resexg ax-mp f1oeq1 cla4ev imaexg bren sylibr
      sylan f1ores f1of1 exp r19.22sdv adantl mpd exp32 19.23adv sylbid
      r19.23aiv imp breq2 cbvrexv sylib ) BAFZGHZAIJZCBKZLCDFZGHZDIJZCVRGHZAIJV
      TWAWDVSWAWDMZAIVRINZVSBVREFZOZEPWFBVRIEQWGWIWFEWGWIWAWDWGWIWALZLWHCRZWBGH
      ZDIJZWDWGWIWMWAWGWKVRKZWMWIDVRWKSWIBVRWHTZWNWIBVRWHUAZWOBVRWHUBUCWOWKWHUD
      ZKWNWHCUEWOWQVRWKBVRWHUFUGUHUIUJUKWJWMWDMWGWJWLWCDIWJWLWCCWKWHCULZOZWLWCW
      JCWKGHZWLWCWSCWKWBUMWSCWKVROZAPWTXAWSAWRWHUNNZWRUNNEUOZWHCUNUPUQCWKVRWRUR
      USCWKAXBWKUNNXCWHCUNUTUQVAVBVCWPWAWSWIBVRCWHVDBVRWHVEVCVCVFVGVHVIVJVKVLVM
      VNWCWEDAIWBVRCGVOVPVQ $.
      $( [24-Jun-98] $)
  $}

  ${
    $d x y A $. $d x y B $.
    $( Lemma for ~ unbnn .  After removing the sucessor of an element from an
       unbounded set of natural numbers, the intersection of the result belongs
       to the original unbounded set. $)
    unblem1 $p |- ( ( ( B (_ om /\ A. x e. om E. y e. B x e. y ) /\
                 A e. B ) -> |^| ( B \ suc A ) e. B ) $=
      ( com wss wel wrex wral wa wcel csuc cdif con0 c0 wceq wn cint omsson
      sstr2 mpan2 difss mpan syl ad2antll ssel peano2b syl6ib imland cv wi
      pm3.26 a1i word nnord ordn2lp imnan sylibr con2d syl6 imp3a jcad eldif
      noel2 sylbir exp3a r19.23adv eleq1 birexdv rcla4v imp syl5 syld imp31
      jca onint eldifi 3syl ) DEFZABGZBDHZAEIZJCDKZJZDCLZMZNFZWFOPQZJWFRZWFKWID
      KWDWGWHVSWGWBWCVSDNFZWGVSENFWJSDENTUAWFDFWJWGDWEUBWFDNTUCUDUEVSWBWCWHVSWB
      WCWHVSWBWCJWBWEEKZJZWHVSWCWKWBVSWCCEKWKDECUFCUGUHUIVSWEBUJZKZBDHZWHWLVSWN
      WHBDVSWMDKZWNWHVSWPWNJZWPWMWEKZQZJZWHVSWQWPWSWQWPUKVSWPWNULUMVSWPWNWSVSWP
      WMEKZWNWSUKZDEWMUFXAWMUNZXBWMUOXCWRWNXCWRWNJQWRWNQUKWMWEUPWRWNUQURUSUDUTV
      AVBWTWMWFKWHWMDWEVCWFWMVDVEUTVFVGWBWKWOWAWOAWEEAUJZWEPVTWNBDXDWEWMVHVIVJV
      KVLVMVFVNVOWFVPWIDWEVQVR $.
      $( [14-Jul-04] $) $( [3-Dec-03] $)
  $}

  ${
    $d x y z w v u A $. $d z w v u F $.
    unblem.1 $e |- ( w e. F -> A. x w e. F ) $.
    unblem.2 $e |- F =
          ( rec ( { <. x , y >. | y = |^| ( A \ suc x ) } , |^| A ) |` om ) $.
    $( Lemma for ~ unbnn .  The value of the function ` F ` belongs to
       the unbounded set of natural numbers ` A ` . $)
    unblem2 $p |- ( ( A (_ om /\ A. w e. om E. v e. A w e. v ) -> ( z e. om ->
                ( F ` z ) e. A ) ) $=
      ( vu cv com wcel wss wel wrex wral wa cfv c0 csuc wceq fveq2 eleq1d weq
      cint con0 wn onint omsson sstr2 mpan2 wex peano1 eleq1 birexdv rcla4v
      mpi df-rex sylib pm3.26 19.22i syl n0 sylibr syl2an cdif copab crdg cres
      frzer fveq1i syl5req ibi wb ax-17 hbfv hbsuc hbdif hbint suceq difeq2d
      inteqd frsucopab cleqcomd exp ibd unblem1 syl5 exp3a finds2 com12 ) CKZLM
      FLNZDEOZEFPZDLQZRZWMGSZFMZWTTGSZFMZJKZGSZFMZXCUAZGSZFMZWRCJWMTUBWSXAFWMTG
      UCUDCJUEWSXDFWMXCGUCUDWMXFUBWSXGFWMXFGUCUDWRFUFZFMZXBFUGNZFTUBUHZXJWNWQFU
      IWNLUGNXKUJFLUGUKULWQEKZFMZEUMZXLWQXNTXMMZRZEUMZXOWQXPEFPZXRWQTLMXSUNWPXS
      DTLDKZTUBWOXPEFXTTXMUOUPUQURXPEFUSUTXQXNEXNXPVAVBVCEFVDVEVFXJXBXJXIXAFXJT
      BKFAKZUAZVGZUFZUBABVHZXIVILVJZSXIXAXIFYEVKTGYFIVLVMUDVNVCXCLMZWRXEXHYGFXD
      UAZVGZUFZFMZXHWRXERYGYKXHYGYKYKXHVOYGYKRZYJXGFYLXGYJABDXIXCYDYJFGXTXIMAVP
      DJOAVPZADYIADFYHXTFMAVPADXDADXCGHYMVQVRVSVTIYAXDUBZYCYIYNYBYHFYAXDWAWBWCW
      DWEUDWFWGDEXDFWHWIWJWKWL $.
      $( [14-Jul-04] $) $( [3-Dec-03] $)

    $( Lemma for ~ unbnn .  The value of the function ` F ` is less than
       its value at a successor. $)
    unblem3 $p |- ( ( A (_ om /\ A. w e. om E. v e. A w e. v ) -> ( z e. om ->
                ( F ` z )  e. ( F ` suc z ) ) ) $=
      ( com wss wel wrex wral wa cv wcel cfv csuc cdif cint con0 unblem2 imp
      wi omsson sstr2 mpan2 ssel anc2li syl ad2antll mpd onmindif wceq unblem1
      exp syld ax-17 hbfv hbsuc hbdif hbint suceq difeq2d inteqd frsucopab
      sylcom eleq2d mpbird ) FJKZDELEFMDJNZOZCPZJQZVNGRZVNSGRZQZVMVOOZVRVPFVPSZ
      TZUAZQZVSFUBKZVPUBQZOZWCVSVPFQZWFVMVOWGABCDEFGHIUCZUDVKWGWFUEZVLVOVKWDWIV
      KJUBKWDUFFJUBUGUHWDWGWEFUBVPUIUJUKULUMFVPUNUKVSVQWBVPVMVOVQWBUOZVMVOWBFQZ
      WJVMVOWGWKWHVMWGWKDEVPFUPUQURVOWKWJABDFUAZVNFAPZSZTZUAWBFGDPZWLQAUSDCLAUS
      ZADWAADFVTWPFQAUSADVPADVNGHWQUTVAVBVCIWMVPUOZWOWAWRWNVTFWMVPVDVEVFVGUQVHU
      DVIVJUQ $.
      $( [14-Jul-04] $) $( [3-Dec-03] $)

    $( Lemma for ~ unbnn .  The function ` F ` maps the set of natural
       numbers one-to-one to set of unbounded natural numbers ` A ` . $)
    unblem4 $p |- ( ( A (_ om /\ A. w e. om E. v e. A w e. v ) ->
                 F : om -1-1-> A ) $=
      ( vz con0 wss com wf wa cv cfv csuc wcel wral wf1 wel wrex omsmo omsson
      sstr2 mpan2 adantr unblem2 r19.21aiv wfn cdif cint wceq copab crdg cres
      frfnom wb fneq1 ax-mp mpbir ffnfv biimpr mpan syl jca unblem3 sylan13 )
      EJKZLEFMZNIOZFPZVKQFPRZILSLEFTELKZCDUADEUBCLSZNZIEFUCVPVIVJVNVIVOVNLJKVIU
      DELJUEUFUGVPVLERZILSZVJVPVQILABICDEFGHUHUIFLUJZVRVJVSBOEAOQUKULUMABUNZEUL
      ZUOLUPZLUJZWAVTUQFWBUMVSWCURHLFWBUSUTVAVJVSVRNILEFVBVCVDVEVFVPVMILABICDEF
      GHVGUIVH $.
      $( [14-Jul-04] $) $( [3-Dec-03] $)
  $}

  ${
    $d x y z w A $.
    unbnn.1 $e |- A e. V $.
    $( Any unbounded subset of natural numbers is equinumerous to the set of
       all natural numbers.  Part of the proof of Theorem 42 of [Suppes]
       p. 151. $)
    unbnn $p |- ( ( A (_ om /\ A. x e. om E. y e. A x e. y ) -> A ~~ om ) $=
      ( vw vz com cdom wbr cen wss wel wrex wral wa sbth cvv wcel wi ssdomg
      ax-mp adantr cv csuc cdif cint wceq copab crdg cres wf1 hbopab1 ax-17
      hbrdg hbres cleqid unblem4 f1dom2g syl sylan13 ) CGHIZGCHIZCGJICGKZABLBCM
      AGNZOZCGPVCVAVDCQRZVCVASDCGQTUAUBVEGCEUCCFUCUDUEUFUGZFEUHZCUFZUIZGUJZUKZV
      BFEABCVKFAVJGFAVIVHVGFEAULAUCZVIRFUMUNVMGRFUMUOVKUPUQVFVLVBSDGCQVKURUAUSU
      T $.
      $( [14-Jul-04] $) $( [3-Dec-03] $)

    $( Version of ~ unbnn that does not require a strict upper bound. $)
    unbnn2 $p |- ( ( A (_ om /\ A. x e. om E. y e. A x (_ y ) -> A ~~ om ) $=
      ( vz com wss wel wrex wral cen wbr cv unbnn csuc wcel wceq sseq1 birexdv
      rcla4v cvv wi visset sucssel ax-mp r19.22si syl6 peano2 syl5 r19.21aiv
      sylan5 ) CFGEBHZBCIZEFJCFKLAMZBMZGZBCIZAFJZEBCDNURUMEFUREMZOZFPZUMUSFPURV
      AUTUOGZBCIZUMUQVCAUTFUNUTQUPVBBCUNUTUORSTVBULBCUSUAPVBULUBEUCUSUOUAUDUEUF
      UGUSUHUIUJUK $.
      $( [17-Jul-04] $) $( [24-Apr-04] $)
  $}

  ${
    $d x y z w A $.
    $( Any set strictly dominated by the class of natural numbers is finite.
       Sufficiency part of Theorem 42 of [Suppes] p. 151.  This theorem does
       not require the Axiom of Infinity. $)
    isfinite2 $p |- ( A ~< om -> E. x e. om A ~~ x ) $=
      ( vy vz vw com cvv wcel csdm wbr cv cen wrex sdomex pm3.27d wss wa wex
      cdom domeng sdomdom syl5bi wel wral wn visset unbnn exp sdomnen nsyli
      ensdomtr ensym sylan syl5 wi csuc con0 word wb cuni ordtri1 ssel2 elon
      sylib an1rs biraldva unissb ralnex bicom 3bitr4g ordunisssuc bitr3d
      omsson sstr2 mpan2 nnord syl2an ssnn peano2b sylanb adantl sylbid
      r19.23adv rexnal syl5ibr syld imp entrt r19.22sdv ad2antrl mpd exp32
      com13 imp3a 19.23adv sylcom mpcom ) FGHZBFIJZBAKZLJZAFMZWSBGHWRBFNOWRWSBC
      KZLJZXCFPZQZCRZXBWRBFSJXGWSCBFGTBFUAUBWSXFXBCWSXDXEXBXEXDWSXBXEXDWSXBXEXD
      WSQZQXCWTLJZAFMZXBXEXHXJXEXHDEUCZEXCMZDFUDZUEZXJXEXCFIJZXNXHXEXMXCFLJZXOX
      EXMXPDEXCCUFZUGUHXCFUIUJXCBLJWSXOXDXCBFUKBXCXQULUMUNXEXLUEZDFMXJXNXEXRXJD
      FXEDKZFHZXRXJUOXEXTQXRXCXSUPZPZXJXCUQPZXSURZXRYBUSXEXTYCYDQZXCUTXSPZXRYBY
      EEKZXSPZEXCUDXKUEZEXCUDZYFXRYEYHYIEXCYCECUCZYDYHYIUSZYGURZYDYLYCYKQZYGXSV
      AYNYGUQHYMXCUQYGVBYGEUFVCVDUMVEVFEXCXSVGYJXRXKEXCVHVIVJXCXSVKVLXEFUQPYCVM
      XCFUQVNVOXSVPVQXTYBXJUOXEXTYBXJYAFHYBXJXTAYAXCVRXSVSVTUHWAWBUHWCXLDFWDWEW
      FWGXDXJXBUOXEWSXDXIXAAFXDXIXABXCWTWHUHWIWJWKWLWMWNWOWPWQ $.
      $( [18-Jul-04] $) $( [24-Apr-04] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d z w F $.
    unfilem1.1 $e |- A e. om $.
    unfilem1.2 $e |- B e. om $.
    unfilem1.3 $e |- F = { <. x , y >. | ( x e. B /\ y = ( A +o x ) ) } $.
    $( Lemma for proving that the union of two finite sets is finite. $)
    unfilem1 $p |- ran F = ( ( A +o B ) \ A ) $=
      ( cv wcel coa co wceq wa copab crn wex cab cdif rnopab rneqi wn eldif
      com wb nnacl mp2an elnn mpan2 wrex wss word ordtri1 nnord syl2an
      nnwordex bitr3d mpan df-rex syl6bb syl nnaord mp3an3 eleq1 sylan9bb
      biimprcd wi cleqcom biimp adantl a1i jcad 19.22dv sylbid imp binegd
      bi2and biimparc ibi nnaword1 ax-mp 3syl mpbid jca sylan 19.23aiv impbi
      bitr biabri 3eqtr4 ) AIZDJZBIZCWKKLZMZNZABOZPWPAQZBREPCDKLZCSZWPABTEWQHUA
      WRBWTWMWTJWMWSJZWMCJZUBZNZWRWMWSCUCXDWRXAXCWRXAXCWKUDJZWNWMMZNZAQZWRXAWMU
      DJZXCXHUEXAWSUDJZXICUDJZDUDJZXJFGCDUFUGWMWSUHUIXIXCXFAUDUJZXHXKXIXCXMUEFX
      KXINCWMUKZXCXMCULZWMULXNXCUEXKXICWMUMCUNZWMUNUOACWMUPUQURXFAUDUSUTVAXAXGW
      PAXAXGWLWOXGWLXAXEWLWNWSJZXFXAXEXLWLXQUEZGXEXLXKXRFWKDCVBVCUIZWNWMWSVDVEV
      FXGWOVGXAXFWOXEXFWOWNWMVHVIVJVKVLVMVNVOWPXDAXQWNCJZUBZNZWOXDWLWOXDYBWOXAX
      QXCYAWMWNWSVDWOXBXTWMWNCVDVPVQVRWLXQYAWLXQWLXEXRWLXLXEGWKDUHUIZXSVAVSWLXE
      YAYCXKXEYAFXKXENZCWNUKZYACWKVTYDWNUDJWNULZYEYAUEZCWKUFWNUNXOYFYGXKXOFXPWA
      CWNUMURWBWCURVAWDWEWFWGWHWIWJ $.
      $( [10-Nov-02] $)

    $( Lemma for proving that the union of two finite sets is finite. $)
    unfilem2 $p |- F : B -1-1-onto-> ( ( A +o B ) \ A ) $=
      ( vz vw coa co cdif wf1o wf1 wfo wa wf cv cfv wceq weq wi wral wfn crn
      oprex fnopab2 unfilem1 pm3.2i df-fo mpbir fof ax-mp wcel opreq2 fvopab4
      cleqan12d com wb nnacan mp3an1 elnn mpan2 syl2an bitrd biimpd rgen2 f1fv
      df-f1o ) DCDKLCMZENDVKEOZDVKEPZQVLVMVLDVKERZISZETZJSZETZUAZIJUBZUCZJDUDID
      UDZQVNWBVMVNVMEDUEZEUFVKUAZQWCWDABDCASZKLZECWEKUGHUHABCDEFGHUIUJDVKEUKULZ
      DVKEUMUNWAIJDVODUOZVQDUOZQZVSVTWJVSCVOKLZCVQKLZUAZVTWHWIVPWKVRWLABVOWFWKD
      EWEVOCKUPHCVOKUGUQABVQWFWLDEWEVQCKUPHCVQKUGUQURVOUSUOZVQUSUOZWMVTUTZWHWIC
      USUOWNWOWPFCVOVQVAVBWHDUSUOZWNGVODVCVDWIWQWOGVQDVCVDVEVFVGVHUJIJDVKEVIULW
      GUJDVKEVJUL $.
      $( [10-Nov-02] $)
  $}

  ${
    $d x y f A $.  $d x y f B $.
    $( Lemma for proving that the union of two finite sets is finite. $)
    unfilem3 $p |- ( ( A e. om /\ B e. om ) -> B ~~ ( ( A +o B ) \ A ) ) $=
      ( vf vx vy com wcel wa coa co cdif cv wf1o wex cen wbr wceq copab cvv
      f1oeq1 cla4egv wfn c0 cded wb opreq1 cleq2d biland biopabdv syl difeq1d
      difeq2 eqtrd f1oeq3 bitrd eleq2 birand f1oeq2 opreq2 3bitrd peano1
      elimel cleqid unfilem2 dedth2h f1ofn wi fnex adantl mpd syl13 oprex
      difexg ax-mp bren sylibr ) AFGZBFGZHZBABIJZAKZCLZMZCNZBWAOPDLZBGZELZAWEIJ
      ZQZHZDERZSGZBWAWKMZWDVSWCWMCWKSBWAWBWKTUAVSWKBUBZWLVSWMWNVQVRWMBVQAUCUDZB
      IJZWOKZWFWGWOWEIJZQZHZDERZMZVRBUCUDZWOXCIJZWOKZWEXCGZWSHZDERZMZABUCUCAWOQ
      ZWMBWAXAMZXBXJWKXAQWMXKUEXJWJWTDEXJWIWSWFXJWHWRWGAWOWEIUFUGUHUIBWAWKXATUJ
      XJWAWQQXKXBUEXJWAWPAKWQXJVTWPAAWOBIUFUKAWOWPULUMWAWQBXAUNUJUOBXCQZXBBWQXH
      MZXCWQXHMZXIXLXAXHQXBXMUEXLWTXGDEXLWFXFWSBXCWEUPUQUIBWQXAXHTUJBXCWQXHURXL
      WQXEQXNXIUEXLWPXDWOBXCWOIUSUKWQXEXCXHUNUJUTDEWOXCXHAUCFVAVBBUCFVAVBXHVCVD
      VEZBWAWKVFUJVRWNWLVGVQBFWKVHVIVJXOVKBWACVTSGWASGABIVLVTASVMVNVOVP $.
      $( [16-Nov-02] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The union of two finite sets is finite.   Part of Corollary 6K of
       [Enderton] p. 144. $)
    unfi $p |- ( ( E. x e. om A ~~ x /\ E. x e. om B ~~ x ) ->
               E. x e. om ( A u. B ) ~~ x ) $=
      ( vz vy cv cen wbr com wrex wa cun cdif reeanv wcel coa co wi unfilem3
      entrt ancoms exp syl imland wceq undif2 a1i wss nnaword1 ssundif sylib
      breq12d cin c0 indif0 pm3.2i unen mpan2 syl5bi syld nnacl breq2 rcla4ev
      r19.23aivv sylbir cbvrexv difss ssfi sylbi sylan5 ) BAFZGHZAIJZCVKGHZAIJZ
      KBCLZDFZGHZDIJZVPVKGHZAIJVMCBMZEFZGHZEIJZVSVOVMWDKVLWCKZEIJAIJVSVLWCAEIIN
      WEVSAEIIVKIOWBIOKZWEVPVKWBPQZGHZVSWFWEVLWAWGVKMZGHZKZWHWFWCWJVLWFWBWIGHZW
      CWJRVKWBSWLWCWJWCWLWJWAWBWITUAUBUCUDWFBWALZVKWILZGHZWHWKWFWMVPWNWGGWMVPUE
      WFBCUFUGWFVKWGUHWNWGUEVKWBUIVKWGUJUKULWKBWAUMUNUEZVKWIUMUNUEZKWOWPWQBCUOV
      KWGUOUPBVKWAWIUQURUSUTWFWGIOZWHVSRVKWBVAWRWHVSVRWHDWGIVQWGVPGVBVCUBUCUTVD
      VEVOCWBGHZEIJZWDVNWSAEIVKWBCGVBVFWTWACUHWDCBVGECWAVHURVIVJVRVTDAIVQVKVPGV
      BVFUK $.
      $( [16-Nov-02] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An unordered pair is finite. $)
    prfi $p |- E. x e. om { A , B } ~~ x $=
      ( cpr cv cen wbr com wrex csn cun snfi unfi mp2an df-pr breq1i birex
      mpbir ) BCDZAEZFGZAHIBJZCJZKZTFGZAHIZUBTFGAHIUCTFGAHIUFABLACLAUBUCMNUAUEA
      HSUDTFBCOPQR $.
      $( [16-Nov-02] $)
  $}

  ${
    $d x y z w v f A $.
    $( Equivalent ways of stating the finite intersection property.  We show
       two ways of saying, "the intersection of elements in every finite
       non-empty subcollection of ` A ` is in ` A ` ". This theorem is
       applicable to a topology, which (among other axioms) is closed under
       finite intersections.  Some texts use the left-hand version of this
       axiom and others the right-hand version, but as our proof here shows,
       their "intuitively obvious" equivalence can be non-trivial to establish
       formally.  $)
    fiint $p |- ( A. x e. A A. y e. A ( x i^i y ) e. A <->
  A. x ( ( ( x (_ A /\ -. x = (/) ) /\ E. y e. om x ~~ y ) -> |^| x e. A ) ) $=
      ( vz vw vv vf cv cin wcel wral wss c0 wceq wn wa cen wbr com wrex cint
      wi wal weq ineq1 eleq1d biraldv cbvralv ineq2 biral csuc breq1 biland
      birimd bialdv visset ensym en0 sylib imran ancoms adantll pm3.24 pm2.21i
      syl ax-gen a1i ax-17 hba1 wf1o wex cima cfv ssel wfo wf wf1 df-f1o
      pm3.27bd fof sucid ffvrn mpan2 3syl syl5 imp adantr crn imassrn wfn ccnv
      wfun w3a f1o2 3simp3 sylbi sseq2d mpbii sstr imrand cres f1of1 sssucid
      f1ores f1oen a1d jcad cvv imaexg ax-mp sseq1 cleq1 binegd bi2and breq2
      inteq bi2imd cla4v sylan9 rcla42v exp3a exp31 com4r imp43 int0 syl6eq
      ineq1d ssv sseqin2 mpbi biimprd pm2.61d2 mpd wb csn cun f1ofn fnsnfv
      uneq2d df-suc imaeq2 imaun eqtr2 foima eqtrd inteqd fvex intunsn syl5eqr
      ad2antlr mpbid exp43 19.23adv bren syl5ib adantlr com13 19.21ad finds2
      ax-4 syl6 exp4a com24 r19.23aiv imp3a 19.21aiv cpr zfpair birexdv prnz
      biantru prfi bitr2 prss bitr4 intpr eleq1i 3imtr3g r19.21adv r19.21aiv
      impbi 3bitr ) AHZBHZIZCJZBCKZACKDHZUXDIZCJZBCKZDCKUXHEHZIZCJZECKZDCKZUXCC
      LZUXCMNZOZPZUXCUXDQRZBSTZPZUXCUAZCJZUBZAUCZUXGUXKADCADUDZUXFUXJBCUYHUXEUX
      ICUXCUXHUXDUEUFUGUHUXKUXODCUXJUXNBECBEUDUXIUXMCUXDUXLUXHUIUFUHUJUXPUYGUXP
      UYFAUXPUXTUYBUYEUYBUXTUXPUYEUYAUXTUXPUYEUBZUBZBSUXDSJZUXDUXCQRZUYJUYAUYKU
      XPUXTUYLUYEUYKUXPUXTUYLUYEUYKUXPUXTUYLPZUYEUBZAUCZUYNUYOUXTMUXCQRZPZUYEUB
      ZAUCZUXTFHZUXCQRZPZUYEUBZAUCZUXTUYTUKZUXCQRZPZUYEUBZAUCZUXPBFUXDMNZUYNUYR
      AVUJUYMUYQUYEVUJUYLUYPUXTUXDMUXCQULUMUNUOBFUDZUYNVUCAVUKUYMVUBUYEVUKUYLVU
      AUXTUXDUYTUXCQULUMUNUOUXDVUENZUYNVUHAVULUYMVUGUYEVULUYLVUFUXTUXDVUEUXCQUL
      UMUNUOUYSUXPUYRAUYQUXRUXSPZUYEUXSUYPVUMUXQUYPUXSVUMUYPUXRUXSUYPUXCMQRUXRM
      UXCAUPZUQUXCURUSUTVAVBVUMUYEUXRVCVDVEVFVGUXPVUDVUIUBUBUYTSJUXPVUDVUHAUXPA
      VHVUCAVIVUGVUDUXPUYEUXQVUFVUDUYIUBZUXSUXQVUFVUOUXQVUEUXCGHZVJZGVKVUOVUFUX
      QVUQVUOGUXQVUQVUDUXPUYEUXQVUQPZVUDUXPPZPZVUPUYTVLZUAZUYTVUPVMZIZCJZUYEVUT
      VVCCJZVVEVURVVFVUSUXQVUQVVFUXQVVCUXCJZVVFVUQUXCCVVCVNVUQVUEUXCVUPVOZVUEUX
      CVUPVPZVVGVUQVUEUXCVUPVQZVVHVUEUXCVUPVRVSZVUEUXCVUPVTVVIUYTVUEJZVVGUYTFUP
      ZWAZVUEUXCUYTVUPWBWCWDWEWFWGVUTVVAMNZVVFVVEUBZUXQVUQVUDUXPVVOOZVVPUBZVUQV
      UDUXPUXQVVRVUQVUDUXPUXQVVRUBVUQVUDPZUXPPUXQVVQVVPVVSUXQVVQPZVVBCJZUXPVVPV
      UQVVTVVACLZVVQPZUYTVVAQRZPZVUDVWAVUQVVTVWCVWDVUQUXQVWBVVQVUQVVAUXCLZUXQVW
      BUBVUQVVAVUPWHZLVWFVUPUYTWIVUQVWGUXCVVAVUQVUPVUEWJZVUPWKWLZVWGUXCNZWMVWJV
      UEUXCVUPWNVWHVWIVWJWOWPWQWRVVAUXCCWSVEWTVUQVWDVVTVUQVVJUYTVVAVUPUYTXAZVJZ
      VWDVUEUXCVUPXBVVJUYTVUELVWLUYTXCVUEUXCUYTVUPXDWCUYTVVAVWKVVMXEWDXFXGVUCVW
      EVWAUBAVVAVUPXHJVVAXHJGUPVUPUYTXHXIXJUXCVVANZVUBVWEUYEVWAVWMUXTVWCVUAVWDV
      WMUXQVWBUXSVVQUXCVVACXKVWMUXRVVOUXCVVAMXLXMXNUXCVVAUYTQXOXNVWMUYDVVBCUXCV
      VAXPUFXQXRXSUXPVWAVVFVVEUXNVVEVVBUXLIZCJDEVVBVVCCCUXHVVBNUXMVWNCUXHVVBUXL
      UEUFUXLVVCNVWNVVDCUXLVVCVVBUIUFXTYAXSYAYBYCYDVVOVVEVVFVVOVVDVVCCVVOVVDXHV
      VCIZVVCVVOVVBXHVVCVVOVVBMUAXHVVAMXPYEYFYGVVCXHLVWOVVCNVVCYHVVCXHYIYJYFUFY
      KYLYMVUQVVEUYEYNUXQVUSVUQVVDUYDCVUQVVAVVCYOZYPZUAUYDVVDVUQVWQUXCVUQVWQVUP
      VUEVLZUXCVUQVWQVVAVUPUYTYOZVLZYPZVWRVUQVWPVWTVVAVUQVWHVWPVWTNZVUEUXCVUPYQ
      VWHVVLVXBVVNVUEUYTVUPYRWCVEYSVWRVUPUYTVWSYPZVLZVXAVUEVXCNVWRVXDNUYTYTVUEV
      XCVUPUVAXJVUPUYTVWSUVBUVCYFVUQVVHVWRUXCNVVKVUEUXCVUPUVDVEUVEUVFVVAVVCUYTV
      UPUVGUVHUVIUFUVJUVKUVLUVMVUEUXCGVUNUVNUVOWFUVPUVQUVRVGUVSUYNAUVTUWAUWBUWC
      UXCUXDBUPUQWEUWDUVQUWEUWFUYGUXODCUYGUXHCJZUXNECUYGVXEUXLCJZUXNUYGUXHUXLUW
      GZCLZVXGMNZOZPZVXGUXDQRZBSTZPZVXGUAZCJZVXEVXFPZUXNUYFVXNVXPUBAVXGDEUWHUXC
      VXGNZUYCVXNUYEVXPVXRUXTVXKUYBVXMVXRUXQVXHUXSVXJUXCVXGCXKVXRUXRVXIUXCVXGMX
      LXMXNVXRUYAVXLBSUXCVXGUXDQULUWIXNVXRUYDVXOCUXCVXGXPUFXQXRVXNVXHVXQVXHVXKV
      XNVXJVXHUXHUXLDUPZUWJUWKVXMVXKBUXHUXLUWLUWKUWMUXHUXLCVXSEUPZUWNUWOVXOUXMC
      UXHUXLVXSVXTUWPUWQUWRYAUWSUWTUXAUXB $.
      $( [19-Nov-02] $)
  $}

$(
  @( If a class is strictly dominated by the class of natural numbers, then
     the Axiom of Infinity holds.  This theorem may seem paradoxical because
     we prove it without invoking the Axiom of Infinity, but in fact (assuming
     ZFC is consistent) the antecedent cannot be proved for any class unless
     the Axiom of Infinity holds. @)
  fin2inf @p |- ( A ~< om -> om e. V ) @=
    ( com cvv wcel csdm wbr wn sdomirr brprc mtbiri a3i ) BCDZABEFZLGMAAEFAHABE
    IJK @.
    @([13-Nov-03]@)
$)

