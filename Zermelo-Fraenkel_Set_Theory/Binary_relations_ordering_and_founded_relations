$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Binary relations, ordering, and founded relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
We have not yet defined relations, but here but introduce a few related
notions we will use to develop ordinals.  The class variable ` R ` is no
different from other class variables, but it reminds us that normally it
represents what we will later call a "relation."
$)

  $( Declare new constant symbols. $)
  $c E $. $( Letter E (for epsilon relation) $)
  $c I $.  $( Letter I (for identity relation) $)
  $c Po $.  $( Partial ordering predicate symbol (read: 'partial ordering'). $)
  $c Or $.  $( Strict ordering predicate symbol (read: 'orders'). $)
  $c sup $.  $( Supremum. $)
  $c Fr $. $( Founded predicate symbol (read: 'founded'). $)
  $c We $. $( Well-ordering predicate symbol (read: 'well orders') $)

  $( Extend wff notation to include the general binary relation predicate.
     Note that the syntax is simply three class symbols in a row.  $)
  wbr $a wff A R B $.

  $( Extend class notation to include abstraction classes of ordered pairs. $)
  copab $a class { <. x , y >. | ph } $.

  $( Extend class notation to include the epsilon relation. $)
  cep $a class E $.

  $( Extend the definition of a class to include identity relation. $)
  cid $a class I $.

  $( Extend wff notation to include the partial ordering predicate.  Read:
     '` R ` is a partial order on ` A ` .' $)
  wpo $a wff R Po A $.

  $( Extend wff notation to include the strict ordering predicate.  Read:
     '` R ` orders ` A ` .' $)
  wor $a wff R Or A $.

  $( Extend class notation to include supremum of class ` A ` .  Here ` R `
     is ordinarily a relation that strictly orders class ` B ` .  For example,
     ` R ` could be 'less than' and ` B ` could be the set of real numbers. $)
  csup $a class sup ( A , B , R ) $.

  $( Extend wff notation to include the founded predicate.  Read:  '` R ` is a
     founded relation on ` A ` .' $)
  wfr $a wff R Fr A $.

  $( Extend wff notation to include the well-ordering predicate.  Read:  '`R `
     well-orders ` A ` .' $)
  wwe $a wff R We A $.

  $( Define a general binary relation.  Note that the syntax is simply three
     class symbols in a row.  Definition 6.18 of [TakeutiZaring]
     p. 29 generalized to arbitrary classes.  Class ` R ` normally denotes a
     relation such as " ` < ` " that compares two classes ` A ` and ` B ` ,
     which might be numbers such as ` 1 ` and ` 2 ` .  This definition is
     well-defined, although not very meaningful, when classes ` A ` and/or
     ` B ` are proper classes (i.e. are not sets).   On the other hand we often
     find uses for this definition when ` R ` is a proper class. $)
  df-br $a |- ( A R B <-> <. A , B >. e. R ) $.

  $( Equality theorem for a binary relation. $)
  breq $p |- ( R = S -> ( A R B <-> A S B ) ) $=
    ( wceq cop wcel wbr eleq2 df-br 3bitr4g ) CDEABFZCGLDGABCHABDHCDLIABCJABDJK
    $.
    $( [4-Jun-95] $)

  $( Equality theorem for binary relation. $)
  breq1 $p |- ( A = B -> ( A R C <-> B R C ) ) $=
    ( wceq cop wcel wbr opeq1 eleq1d df-br 3bitr4g ) ABEZACFZDGBCFZDGACDHBCDHMN
    ODABCIJACDKBCDKL $.
    $( [31-Dec-93] $)

  $( Equality theorem for binary relation. $)
  breq2 $p |- ( A = B -> ( C R A <-> C R B ) ) $=
    ( wceq cop wcel wbr opeq2 eleq1d df-br 3bitr4g ) ABEZCAFZDGCBFZDGCADHCBDHMN
    ODABCIJCADKCBDKL $.
    $( [31-Dec-93] $)

  $( Equality theorem for binary relation. $)
  breq12 $p |- ( ( A = B /\ C = D ) -> ( A R C <-> B R D ) ) $=
    ( wceq wbr breq1 breq2 sylan9bb ) ABFACEGBCEGCDFBDEGABCEHCDBEIJ $.
    $( [8-Feb-96] $)

  ${
    breq1i.1 $e |- A = B $.
    $( Equality inference for binary relation. $)
    breq1i $p |- ( A R C <-> B R C ) $=
      ( wceq wbr wb breq1 ax-mp ) ABFACDGBCDGHEABCDIJ $.
      $( [8-Feb-96] $)

    $( Equality inference for binary relation. $)
    breq2i $p |- ( C R A <-> C R B ) $=
      ( wceq wbr wb breq2 ax-mp ) ABFCADGCBDGHEABCDIJ $.
      $( [8-Feb-96] $)

    ${
      breq12i.2 $e |- C = D $.
      $( Equality inference for binary relation. $)
      breq12i $p |- ( A R C <-> B R D ) $=
        ( wbr breq1i breq2i bitr ) ACEHBCEHBDEHABCEFICDBEGJK $.
        $( [8-Feb-96] $)
    $}
  $}

  ${
    breq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for binary relation. $)
    breq1d $p |- ( ph -> ( A R C <-> B R C ) ) $=
      ( wceq wbr wb breq1 syl ) ABCGBDEHCDEHIFBCDEJK $.
      $( [8-Feb-96] $)

    $( Equality deduction for binary relation. $)
    breq2d $p |- ( ph -> ( C R A <-> C R B ) ) $=
      ( wceq wbr wb breq2 syl ) ABCGDBEHDCEHIFBCDEJK $.
      $( [8-Feb-96] $)

    ${
      breq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for binary relation. $)
      breq12d $p |- ( ph -> ( A R C <-> B R D ) ) $=
        ( wbr breq1d breq2d bitrd ) ABDFICDFICEFIABCDFGJADECFHKL $.
        $( [8-Feb-96] $)
    $}

    ${
      breqan12i.2 $e |- ( ps -> C = D ) $.
      $( Equality deduction for binary relation. $)
      breqan12d $p |- ( ( ph /\ ps ) -> ( A R C <-> B R D ) ) $=
        ( wceq wbr wb breq12 syl2an ) CDJEFJCEGKDFGKLABCDEFGMHIN $.
        $( [8-Feb-96] $)

      $( Equality deduction for binary relation. $)
      breqan12rd $p |- ( ( ps /\ ph ) -> ( A R C <-> B R D ) ) $=
        ( wbr wb breqan12d ancoms ) ABCEGJDFGJKABCDEFGHILM $.
        $( [8-Feb-96] $)
    $}
  $}

  ${
    eqbrtr.1 $e |- A = B $.
    eqbrtr.2 $e |- B R C $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtr $p |- A R C $=
      ( wbr breq1i mpbir ) ACDGBCDGFABCDEHI $.
      $( [5-Aug-93] $)
  $}

  ${
    eqbrtrd.1 $e |- ( ph -> A = B ) $.
    eqbrtrd.2 $e |- ( ph -> B R C ) $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrd $p |- ( ph -> A R C ) $=
      ( wbr breq1d mpbird ) ABDEHCDEHGABCDEFIJ $.
      $( [8-Oct-99] $)
  $}

  ${
    eqbrtrr.1 $e |- A = B $.
    eqbrtrr.2 $e |- A R C $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrr $p |- B R C $=
      ( cleqcomi eqbrtr ) BACDABEGFH $.
      $( [5-Aug-93] $)
  $}

  ${
    eqbrtrrd.1 $e |- ( ph -> A = B ) $.
    eqbrtrrd.2 $e |- ( ph -> A R C ) $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrrd $p |- ( ph -> B R C ) $=
      ( cleqcomd eqbrtrd ) ACBDEABCFHGI $.
      $( [24-Oct-99] $)
  $}

  ${
    breqtr.1 $e |- A R B $.
    breqtr.2 $e |- B = C $.
    $( Substitution of equal classes into a binary relation. $)
    breqtr $p |- A R C $=
      ( wbr breq2i mpbi ) ABDGACDGEBCADFHI $.
      $( [5-Aug-93] $)
  $}

  ${
    breqtrd.1 $e |- ( ph -> A R B ) $.
    breqtrd.2 $e |- ( ph -> B = C ) $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrd $p |- ( ph -> A R C ) $=
      ( wbr breq2d mpbid ) ABCEHBDEHFACDBEGIJ $.
      $( [24-Oct-99] $)
  $}

  ${
    breqtrr.1 $e |- A R B $.
    breqtrr.2 $e |- C = B $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrr $p |- A R C $=
      ( cleqcomi breqtr ) ABCDECBFGH $.
      $( [5-Aug-93] $)
  $}

  ${
    breqtrrd.1 $e |- ( ph -> A R B ) $.
    breqtrrd.2 $e |- ( ph -> C = B ) $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrrd $p |- ( ph -> A R C ) $=
      ( cleqcomd breqtrd ) ABCDEFADCGHI $.
      $( [24-Oct-99] $)
  $}

  ${
    3brtr3.1 $e |- A R B $.
    3brtr3.2 $e |- A = C $.
    3brtr3.3 $e |- B = D $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3 $p |- C R D $=
      ( eqbrtrr breqtr ) CBDEACBEGFIHJ $.
      $( [11-Aug-99] $)
  $}

  ${
    3brtr4.1 $e |- A R B $.
    3brtr4.2 $e |- C = A $.
    3brtr4.3 $e |- D = B $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr4 $p |- C R D $=
      ( eqbrtr breqtrr ) CBDECABEGFIHJ $.
      $( [11-Aug-99] $)
  $}

  ${
    3brtr3d.1 $e |- ( ph -> A R B ) $.
    3brtr3d.2 $e |- ( ph -> A = C ) $.
    3brtr3d.3 $e |- ( ph -> B = D ) $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3d $p |- ( ph -> C R D ) $=
      ( wbr breq12d mpbid ) ABCFJDEFJGABDCEFHIKL $.
      $( [18-Oct-99] $)
  $}

  ${
    3brtr3g.1 $e |- ( ph -> A R B ) $.
    3brtr3g.2 $e |- A = C $.
    3brtr3g.3 $e |- B = D $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3g $p |- ( ph -> C R D ) $=
      ( wbr breq12i sylib ) ABCFJDEFJGBDCEFHIKL $.
      $( [16-Jan-97] $)
  $}

  ${
    3brtr4g.1 $e |- ( ph -> A R B ) $.
    3brtr4g.2 $e |- C = A $.
    3brtr4g.3 $e |- D = B $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr4g $p |- ( ph -> C R D ) $=
      ( wbr breq12i sylibr ) ABCFJDEFJGDBECFHIKL $.
      $( [16-Jan-97] $)
  $}

  ${
    syl5eqbr.1 $e |- ( ph -> A R B ) $.
    syl5eqbr.2 $e |- C = A $.
    $( A chained equality inference for a binary relation. $)
    syl5eqbr $p |- ( ph -> C R B ) $=
      ( cleqid 3brtr4g ) ABCDCEFGCHI $.
      $( [11-Oct-99] $)
  $}

  ${
    syl5eqbrr.1 $e |- ( ph -> A R B ) $.
    syl5eqbrr.2 $e |- A = C $.
    $( A chained equality inference for a binary relation. $)
    syl5eqbrr $p |- ( ph -> C R B ) $=
      ( cleqid 3brtr3g ) ABCDCEFGCHI $.
      $( [?] $) $( [17-Sep-04] $)
  $}

  ${
    syl5breq.1 $e |- ( ph -> A = B ) $.
    syl5breq.2 $e |- C R A $.
    $( A chained equality inference for a binary relation. $)
    syl5breq $p |- ( ph -> C R B ) $=
      ( wbr breq2d mpbii ) ADBEHDCEHGABCDEFIJ $.
      $( [11-Oct-99] $)
  $}

  ${
    syl6eqbr.1 $e |- ( ph -> A = B ) $.
    syl6eqbr.2 $e |- B R C $.
    $( A chained equality inference for a binary relation. $)
    syl6eqbr $p |- ( ph -> A R C ) $=
      ( wbr breq1d mpbiri ) ABDEHCDEHGABCDEFIJ $.
      $( [12-Oct-99] $)
  $}

  ${
    syl6breq.1 $e |- ( ph -> A R B ) $.
    syl6breq.2 $e |- B = C $.
    $( A chained equality inference for a binary relation. $)
    syl6breq $p |- ( ph -> A R C ) $=
      ( cleqid 3brtr3g ) ABCBDEFBHGI $.
      $( [11-Oct-99] $)
  $}


  ${
    ssbrd.1 $e |- ( ph -> A (_ B ) $.
    $( Deduction from a subclass relationship of binary relations. $)
    ssbrd $p |- ( ph -> ( C A D -> C B D ) ) $=
      ( cop wcel wbr sseld df-br 3imtr4g ) ADEGZBHMCHDEBIDECIABCMFJDEBKDECKL $.
      $( [14-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y R $.  $d x y $.
    hbbr.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbbr.2 $e |- ( y e. R -> A. x y e. R ) $.
    hbbr.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for binary relation. $)
    hbbr $p |- ( A R B -> A. x A R B ) $=
      ( cop wcel wal wbr hbop hbel df-br bial 3imtr4 ) CDIZEJZSAKCDELZTAKABBREA
      BCDFHMGNCDEOZTSAUAPQ $.
      $( [1-Sep-99] $)
  $}

  ${
    $d x z $.  $d y z $.  $d R z $.
    $( Relationship between a binary relation and a class abstraction. $)
    brab1 $p |- ( x R y <-> x e. { z | z R y } ) $=
      ( cv wbr cab wcel visset breq1 elab bicom ) AEZCEZBEZDFZCGHMODFZPQCMAINMO
      DJKL $.
      $( [7-Apr-94] $)
  $}

  $( A property of proper class as the second argument of a binary relation. $)
  brprc $p |- ( -. B e. V -> ( A R B <-> A R A ) ) $=
    ( cvv wcel wn cop wbr opprc2 eleq1d df-br 3bitr4g ) BDEFZABGZCEAAGZCEABCHAA
    CHMNOCABIJABCKAACKL $.
    $( [14-Jun-98] $)

  ${
    $d x z $.  $d y z $.  $d z ph $.
    $( Define abstraction classes of ordered pairs.  Definition 3.3 of
       [Monk1] p. 34.  Normally ` x ` and ` y ` are distinct, although the
       definition doesn't strictly require it. $)
    df-opab $a |- { <. x , y >. | ph } =
                  { z | E. x E. y ( z = <. x , y >. /\ ph ) } $.
  $}

  ${
    $d x y z w v $.  $d ph z w v $.  $( ` z w v ` are dummy. $)
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61. $)
    opabid $p |- ( <. x , y >. e. { <. x , y >. | ph } <-> ph ) $=
      ( vz vw vv cv cop wceq wa wex cab wcel copab clelab df-opab eleq2i wsb
      weq 19.41v anass biex cleqcom opex eqvinc visset opth 3bitr3 biran
      3bitr3r bi2ex sbel2x excom exdistr2 3bitr4 ax-17 hbs1 hban hbex opeq1
      cleq2d sbequ12 bi2and biexdv cbvex opeq2 bitr bilan bitr4 ) BGZCGZHZDGZVL
      IZAJZCKZBKZDLZMVNVQJZDKZVLABCNZMAVQDVLOWAVRVLABCDPQAVNVMEGZFGZHZIZABERZCF
      RZJZFKZEKZJZDKZVTEBSFCSJZWGJZFKEKVNWHJZDKZFKZEKZAWLWNWPEFVNWEJZWGJZDKWSDK
      ZWGJWPWNWSWGDTWTWODVNWEWGUAUBXAWMWGVLWDIWDVLIXAWMVLWDUCDVLWDVJVKUDUEWBWCV
      JVKEUFFUFCUFUGUHUIUJUKAEFBCULWOFKZEKDKXBDKZEKWLWRXBDEUMVNWHDEFUNXCWQEWODF
      UMUBUHUOVSWKDVQWJVNVQVMWBVKHZIZWFJZCKZEKWJVPXGBEVPEUPXFBCXEWFBXEBUPABEUQU
      RUSBESZVOXFCXHVNXEAWFXHVLXDVMVJWBVKUTVAABEVBVCVDVEXGWIEXFWHCFXFFUPWEWGCWE
      CUPWFCFUQURCFSZXEWEWFWGXIXDWDVMVKWCWBVFVAWFCFVBVCVEUBVGVHUBVIUO $.
      $( [14-Apr-95] $)
  $}

  ${
    $d x y z R $.  $( ` z ` is dummy. $)
    $( The collection of ordered pairs in a class is a subclass of it. $)
    opabss $p |- { <. x , y >. | x R y } (_ R $=
      ( vz cv wbr copab wcel cab cop wceq wa wex df-opab eleq1 biimpar df-br
      sylan5b 19.23aivv ss2abi eqsstr abid2 sseqtr ) AEZBEZCFZABGZDEZCHZDIZCUGU
      HUDUEJZKZUFLZBMAMZDIUJUFABDNUNUIDUMUIABULUKCHZUIUFULUIUOUHUKCOPUDUECQRSTU
      ADCUBUC $.
      $( [27-Dec-96] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z ph $.  $d z ps $.  $d z ch $.
    biopabd.1 $e |- ( ph -> A. x ph ) $.
    biopabd.2 $e |- ( ph -> A. y ph ) $.
    biopabd.3 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal ordered-pair abstraction classes (deduction
       rule). $)
    biopabd $p |- ( ph -> { <. x , y >. | ps } = { <. x , y >. | ch } ) $=
      ( vz cv cop wceq wa wex cab copab ax-17 biland biexd biabd df-opab
      3eqtr4g ) AIJDJEJKLZBMZENZDNZIOUCCMZENZDNZIOBDEPCDEPAUFUIIAIQAUEUHDFAUDUG
      EGABCUCHRSSTBDEIUACDEIUAUB $.
      $( [24-Feb-04] $) $( [21-Feb-04] $)
  $}

  ${
    $d x y z ph $.  $d z ps $.  $d z ch $.  $( ` z ` is dummy. $)
    biopabdv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal ordered-pair abstraction classes (deduction
       rule). $)
    biopabdv $p |- ( ph -> { <. x , y >. | ps } = { <. x , y >. | ch } ) $=
      ( ax-17 biopabd ) ABCDEADGAEGFH $.
      $( [15-May-95] $)
  $}

  ${
    $d x y z $.  $d z ph $.  $d z ps $.  $( ` z ` is dummy. $)
    biopab.1 $e |- ( ph <-> ps ) $.
    $( Equivalent wff's yield equal abstraction classes. $)
    biopab $p |- { <. x , y >. | ph } = { <. x , y >. | ps } $=
      ( vz weq copab wceq cv cleqid wb a1i biopabdv ax-mp ) FFGZACDHBCDHIFJKPAB
      CDABLPEMNO $.
      $( [15-May-95] $)
  $}

  ${
    $d x y z w v $.  $d v ph $.  $d v ps $.  $( ` v ` is dummy. $)
    cbvopab.1 $e |- ( ph -> A. z ph ) $.
    cbvopab.2 $e |- ( ph -> A. w ph ) $.
    cbvopab.3 $e |- ( ps -> A. x ps ) $.
    cbvopab.4 $e |- ( ps -> A. y ps ) $.
    cbvopab.5 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables in an ordered pair abstraction,
       using implicit substitution. $)
    cbvopab $p |- { <. x , y >. | ph } = { <. z , w >. | ps } $=
      ( vv cv cop wceq wa wex cab copab ax-17 hban weq opeq12 cleq2d bi2and
      cbvex2 biabi df-opab 3eqtr4 ) LMZCMZDMZNZOZAPZDQCQZLRUJEMZFMZNZOZBPZFQEQZ
      LRACDSBEFSUPVBLUOVACDEFUNAEUNETGUAUNAFUNFTHUAUTBCUTCTIUAUTBDUTDTJUACEUBDF
      UBPZUNUTABVCUMUSUJUKULUQURUCUDKUEUFUGACDLUHBEFLUHUI $.
      $( [14-Sep-03] $)
  $}

  ${
    $d x y z w $.  $d z w v ph $.  $d x y v ps $.  $( ` v ` is dummy. $)
    cbvopabv.1 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables in an ordered pair abstraction,
       using implicit substitution. $)
    cbvopabv $p |- { <. x , y >. | ph } = { <. z , w >. | ps } $=
      ( ax-17 cbvopab ) ABCDEFAEHAFHBCHBDHGI $.
      $( [15-Oct-96] $)
  $}

$(
  @{
    @d x y z w @.  @d w ph @.  @d w ps @.
    cbvopab1.1 @e |- ( ph -> A. z ph ) @.
    cbvopab1.2 @e |- ( ps -> A. x ps ) @.
    cbvopab1.3 @e |- ( x = z -> ( ph <-> ps ) ) @.
    @( Change first bound variable in an ordered pair abstraction,
       using explicit substitution. @)
    cbvopab1 @p |- { <. x , y >. | ph } = { <. z , y >. | ps } @=
      ( vw cv cop wceq wa wex cab copab ax-17 hban hbex weq opeq1 cleq2d
      bi2and biexdv cbvex biabi df-opab 3eqtr4 ) IJZCJZDJZKZLZAMZDNZCNZIOUIEJZU
      KKZLZBMZDNZENZIOACDPBEDPUPVBIUOVACEUNEDUMAEUMEQFRSUTCDUSBCUSCQGRSCETZUNUT
      DVCUMUSABVCULURUIUJUQUKUAUBHUCUDUEUFACDIUGBEDIUGUH @.
      @([xxx]@)
  @}
$)

$(
  @{
    @d x y z w @.  @d z w ph @.
    @( Change first bound variable in an ordered pair abstraction,
       using explicit substitution. @)
    cbvopab1s @p |- { <. x , y >. | ph } = { <. z , y >. | [ z / x ] ph } @=
      ( vw cv cop wceq wa wex cab wsb copab ax-17 hbs1 hban hbex weq opeq1
      cleq2d sbequ12 bi2and biexdv cbvex biabi df-opab 3eqtr4 ) EFZBFZCFZGZHZAI
      ZCJZBJZEKUHDFZUJGZHZABDLZIZCJZDJZEKABCMUSDCMUOVBEUNVABDUNDNUTBCURUSBURBNA
      BDOPQBDRZUMUTCVCULURAUSVCUKUQUHUIUPUJSTABDUAUBUCUDUEABCEUFUSDCEUFUG @.
      @([xxx]@)
  @}
$)

  ${
    $d x y z w $.  $d z w ph $.  $d x w ps $.  $( ` w ` is dummy. $)
    cbvopab1v.1 $e |- ( x = z -> ( ph <-> ps ) ) $.
    $( Rule used to change the first bound variable in an ordered pair
       abstraction, using implicit substitution. $)
    cbvopab1v $p |- { <. x , y >. | ph } = { <. z , y >. | ps } $=
      ( vw cv cop wceq wa wex cab copab weq opeq1 cleq2d bi2and biexdv cbvexv
      biabi df-opab 3eqtr4 ) GHZCHZDHZIZJZAKZDLZCLZGMUDEHZUFIZJZBKZDLZELZGMACDN
      BEDNUKUQGUJUPCECEOZUIUODURUHUNABURUGUMUDUEULUFPQFRSTUAACDGUBBEDGUBUC $.
      $( [31-Jul-03] $)
  $}

  ${
    $d x y z w $.  $d z w ph $.  $d y w ps $.  $( ` w ` is dummy. $)
    cbvopab2v.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Rule used to change the second bound variable in an ordered pair
       abstraction, using implicit substitution. $)
    cbvopab2v $p |- { <. x , y >. | ph } = { <. x , z >. | ps } $=
      ( vw cv cop wceq wa wex cab copab weq opeq2 cleq2d bi2and cbvexv biex
      biabi df-opab 3eqtr4 ) GHZCHZDHZIZJZAKZDLZCLZGMUDUEEHZIZJZBKZELZCLZGMACDN
      BCENUKUQGUJUPCUIUODEDEOZUHUNABURUGUMUDUFULUEPQFRSTUAACDGUBBCEGUBUC $.
      $( [2-Sep-99] $)
  $}

  ${
    $d x y z A $.  $d z ph $.  $( ` z ` is dummy. $)
    $( Membership in abstraction class of pairs. $)
    elopab $p |- ( A e. { <. x , y >. | ph } <->
                 E. x E. y ( A = <. x , y >. /\ ph ) ) $=
      ( vz copab wcel cvv cv cop wceq wa wex elisset opex eleq1 mpbiri adantr
      19.23aivv cleq1 birand bi2exdv df-opab cleqabi vtoclbg pm5.21nii ) DABCFZ
      GZDHGZDBIZCIZJZKZALZCMBMZDUGNUNUIBCUMUIAUMUIULHGUJUKODULHPQRSEIZUGGUPULKZ
      ALZCMBMZUHUOEDHUPDUGPUPDKZURUNBCUTUQUMAUPDULTUAUBUSEUGABCEUCUDUEUF $.
      $( [24-Mar-98] $)
  $}

  ${
    $d x y z w $.
    hbopab.1 $e |- ( ph -> A. z ph ) $.
    $( Bound-variable hypothesis builder for abstraction class. $)
    hbopab $p |- ( w e. { <. x , y >. | ph } ->
                 A. z w e. { <. x , y >. | ph } ) $=
      ( cv cop wceq wa wex wal copab wcel ax-17 hban hbex elopab bial 3imtr4 )
      EGZBGCGHIZAJZCKZBKZUEDLUAABCMNZUFDLUDDBUCDCUBADUBDOFPQQABCUARZUFUEDUGST
      $.
      $( [1-Sep-99] $)
  $}

  ${
    $d x y w $.  $d x z $.  $d w ph $.  $( ` w ` is dummy. $)
    $( The abstraction variables in an abstraction class of pairs are not
       free. $)
    hbopab1 $p |- ( z e. { <. x , y >. | ph } ->
                 A. x z e. { <. x , y >. | ph } ) $=
      ( vw cv cop wceq wa wex cab wcel wal copab hbe1 hbab df-opab eleq2i bial
      3imtr4 ) DFZEFBFCFGHAICJZBJZEKZLZUEBMUAABCNZLZUGBMUCBEDUBBOPUFUDUAABCEQRZ
      UGUEBUHST $.
      $( [16-May-95] $)
  $}

  ${
    $d x y w $.  $d y z $.  $d w ph $.  $( ` w ` is dummy. $)
    $( The abstraction variables in an abstraction class of pairs are not
       free. $)
    hbopab2 $p |- ( z e. { <. x , y >. | ph } ->
                 A. y z e. { <. x , y >. | ph } ) $=
      ( vw cv cop wceq wa wex cab wcel wal copab hbe1 hbex hbab df-opab eleq2i
      bial 3imtr4 ) DFZEFBFCFGHAIZCJZBJZEKZLZUGCMUBABCNZLZUICMUECEDUDCBUCCOPQUH
      UFUBABCERSZUIUGCUJTUA $.
      $( [16-May-95] $)
  $}


  ${
    $d x y z w v $.  $d v ph $.
    $( The law of concretion in terms of substitutions. $)
    opabsb $p |- ( <. z , w >. e. { <. x , y >. | ph }
                 <-> [ w / y ] [ z / x ] ph ) $=
      ( vv weq wex cv cop copab wcel wsb wb a9e ax-17 hbopab2 hbel hbs1 hbbi
      wi hbopab1 hbsb hbim wa opeq12 eleq1d opabid syl5bbr sbequ12 sylan9bb
      bitr3d exp 19.23ai ax-mp ) CEGZCHDIZEIZJZABCKZLZABDMZCEMZNZCEOUPVDCVAVCCC
      FFUSUTFIUSLZCPABCFQRVBCESTBDGZBHUPVDUAZBDOVFVGBUPVDBUPBPVAVCBBFFUSUTVEBPA
      BCFUBRVBCEBABDSUCTUDVFUPVDVFUPUEZAVAVCVHBIZCIZJZUTLVAAVHVKUSUTVIVJUQURUFU
      GABCUHUIVFAVBUPVCABDUJVBCEUJUKULUMUNUOUNUO $.
      $( [30-Sep-02] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y ch $.  $d z ph $.
        $( ` z ` is dummy. $)
    opelopabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopabg $p |- ( ( A e. C /\ B e. D ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) ) $=
      ( vz wcel wa cv wceq wex cop copab wb elex im2an eeanv sylibr ax-17
      hbopab1 hbel hbbi hbopab2 opeq12 eleq1d opabid syl5bbr sylan9bb bitr3d
      19.23ai syl ) FHMZGIMZNZDOZFPZEOZGPZNZEQZDQZFGRZADESZMZCTZUTVBDQZVDEQZNVG
      URVLUSVMDFHUAEGIUAUBVBVDDEUCUDVFVKDVJCDDLLVHVILOVHMZDUEADELUFUGCDUEUHVEVK
      EVJCEELLVHVIVNEUEADELUIUGCEUEUHVEAVJCVEVAVCRZVIMVJAVEVOVHVIVAVCFGUJUKADEU
      LUMVBABVDCJKUNUOUPUPUQ $.
      $( [28-May-95] $)

    ${
      brabg.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brabg $p |- ( ( A e. C /\ B e. D ) -> ( A R B <-> ch ) ) $=
        ( wcel wa cop copab wbr opelopabg df-br eleq2i bitr syl5bb ) FHNGINOFGP
        ZADEQZNZCFGJRZABCDEFGHIKLSUGUDJNUFFGJTJUEUDMUAUBUC $.
        $( [16-Aug-99] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.
    opelopab.1 $e |- A e. V $.
    opelopab.2 $e |- B e. V $.
    opelopab.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopab $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( cvv wcel cop copab wb opelopabg mp2an ) FLMGLMFGNADEOMCPHIABCDEFGLLJKQR
      $.
      $( [16-May-95] $)

    ${
      brab.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brab $p |- ( A R B <-> ch ) $=
        ( cvv wcel wbr wb brabg mp2an ) FNOGNOFGHPCQIJABCDEFGNNHKLMRS $.
        $( [16-Aug-99] $)
    $}
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.  $( ` z ` is dummy. $)
    $( Equivalence of abstraction subclass and implication. $)
    ssopab2 $p |- ( { <. x , y >. | ph } (_ { <. x , y >. | ps } <->
               A. x A. y ( ph -> ps ) ) $=
      ( vz copab wss wi wal hbopab1 hbss hbopab2 cv cop wceq wex opex isseti
      wa cab copsexg bi2imd ss2ab ax-4 sylbi syl5bir df-opab sseq12i syl5ib
      19.23aiv ax-mp 19.21ai hba1 imland 19.22d a4s 19.21aiv sylibr 3sstr4g
      impbi ) ACDFZBCDFZGZABHZDIZCIZVCVECCEVAVBACDEJBCDEJKVCVDDDEVAVBACDELBCDEL
      KEMZCMZDMZNZOZEPVCVDHZEVJVHVIQRVKVLEVKVKASZDPZCPZETZVKBSZDPZCPZETZGZVDVCV
      KVDVOVSHZWAVKAVOBVSACDVGUABCDVGUAUBWAWBEIZWBVOVSEUCZWBEUDUEUFVAVPVBVTACDE
      UGZBCDEUGZUHUIUJUKULULVFVPVTVAVBVFWCWAVFWBEVFVNVRCVECUMVEVNVRHCVEVMVQDVDD
      UMVEABVKVDDUDUNUOUPUOUQWDURWEWFUSUT $.
      $( [27-Dec-96] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.  $( ` z ` is dummy. $)
    ssopab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of abstraction subclass from implication. $)
    ssopab2i $p |- { <. x , y >. | ph } (_ { <. x , y >. | ps } $=
      ( copab wss wi wal ssopab2 ax-gen mpgbir ) ACDFBCDFGABHZDICABCDJMDEKL $.
      $( [5-Apr-95] $)
  $}

  ${
    $d x z $.  $d y z $.  $d ph z $.  $d ps z $.
    $( Union of two ordered pair class abstractions. $)
    unopab $p |- ( { <. x , y >. | ph } u. { <. x , y >. | ps } ) =
               { <. x , y >. | ( ph \/ ps ) } $=
      ( vz cv cop wceq wa wex cab cun wo copab unab 19.43 andi biex bitr2
      bitr3 biabi eqtr df-opab uneq12i 3eqtr4 ) EFCFDFGHZAIZDJZCJZEKZUFBIZDJZCJ
      ZEKZLZUFABMZIZDJZCJZEKZACDNZBCDNZLUPCDNUOUIUMMZEKUTUIUMEOVCUSEVCUHULMZCJU
      SUHULCPVDURCURUGUKMZDJVDUQVEDUFABQRUGUKDPSRTUAUBVAUJVBUNACDEUCBCDEUCUDUPC
      DEUCUE $.
      $( [30-Sep-02] $)
  $}

  ${
    $d x y $.
    $( Define the epsilon relation.  Similar to Definition 6.22 of
       [TakeutiZaring] p. 30. $)
    df-eprel $a |- E = { <. x , y >. | x e. y } $.
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    epelc.1 $e |- A e. V $.
    epelc.2 $e |- B e. V $.
    $( The epsilon relation and the membership relation are the same. $)
    epelc $p |- ( A E B <-> A e. B ) $=
      ( vx vy wel cv wcel cep eleq1 eleq2 df-eprel brab ) EFGAFHZIABIEFABJCDEHA
      OKOBALEFMN $.
      $( [13-Aug-95] $)
  $}

  ${
    $( The epsilon relation and the membership relation are the same. $)
    epel $p |- ( x E y <-> x e. y ) $=
      ( cv visset epelc ) ACBCADBDE $.
      $( [13-Aug-95] $)
  $}

  ${
    $d x y $.
    $( Define the identity relation.  Definition 9.15 of [Quine] p. 64. $)
    df-id $a |- I = { <. x , y >. | x = y } $.
  $}

  ${
    $d x y A $.  $d x y B $.
    $( For sets, the identity relation is the same as equality. $)
    ideqg $p |- ( ( A e. C /\ B e. D ) -> ( A I B <-> A = B ) ) $=
      ( vx vy weq cv wceq cid cleq1 cleq2 df-id brabg ) EFGAFHZIABIEFABCDJEHAOK
      OBALEFMN $.
      $( [2-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    ideq.1 $e |- A e. V $.
    ideq.2 $e |- B e. V $.
    $( For sets, the identity relation is the same as equality. $)
    ideq $p |- ( A I B <-> A = B ) $=
      ( cvv wcel cid wbr wceq wb ideqg mp2an ) AEFBEFABGHABIJCDABEEKL $.
      $( [13-Aug-95] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    $( Define a partial order relation.  Definition of [Enderton] p. 168. $)
    df-po $a |- ( R Po A <-> A. x e. A A. y e. A A. z e. A
                ( -. x R x /\ ( ( x R y /\ y R z ) -> x R z ) ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Subset theorem for the partial ordering predicate. $)
    poss $p |- ( A (_ B -> ( R Po B -> R Po A ) ) $=
      ( vx vy vz wss cv wbr wn wa wi wral wpo wcel w3a wal ssel im2and df-3an
      3imtr4g syl4d 19.20dv r3al df-po ) ABGZDHZUGCIJUGEHZCIUHFHZCIKUGUICILKZFB
      MEBMDBMZUJFAMEAMDAMZBCNACNUFUGBOZUHBOZUIBOZPZUJLZFQZEQZDQUGAOZUHAOZUIAOZP
      ZUJLZFQZEQZDQUKULUFUSVFDUFURVEEUFUQVDFUFVCUPUJUFUTVAKZVBKUMUNKZUOKVCUPUFV
      GVHVBUOUFUTUMVAUNABUGRABUHRSABUIRSUTVAVBTUMUNUOTUAUBUCUCUCUJDEFBBBUDUJDEF
      AAAUDUADEFBCUEDEFACUEUA $.
      $( [27-Mar-97] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Equality theorem for partial ordering predicate. $)
    poeq1 $p |- ( R = S -> ( R Po A <-> S Po A ) ) $=
      ( vx vy vz wceq cv wbr wn wa wi wral wpo breq binegd bi2and bi2imd
      biraldv df-po 3bitr4g ) BCGZDHZUCBIZJZUCEHZBIZUFFHZBIZKZUCUHBIZLZKZFAMZEA
      MZDAMUCUCCIZJZUCUFCIZUFUHCIZKZUCUHCIZLZKZFAMZEAMZDAMABNACNUBUOVEDAUBUNVDE
      AUBUMVCFAUBUEUQULVBUBUDUPUCUCBCOPUBUJUTUKVAUBUGURUIUSUCUFBCOUFUHBCOQUCUHB
      CORQSSSDEFABTDEFACTUA $.
      $( [27-Mar-97] $)
  $}

  $( Equality theorem for partial ordering predicate. $)
  poeq2 $p |- ( A = B -> ( R Po A <-> R Po B ) ) $=
    ( wceq wpo wss wa wi wb poss im2an eqss bi 3imtr4 bicomd ) ABDZBCEZACEZABFZ
    BAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [27-Mar-97] $)

  ${
    $d x y z R $. $d x y z A $.
    $d x y z B $.  $d x y z C $.  $d x y z D $.  $( ` x y z ` are dummy. $)
    $( Properties of partial order relation in class notation. $)
    pocl $p |- ( R Po A -> ( ( B e. A /\ C e. A /\ D e. A ) ->
               ( -. B R B /\ ( ( B R C /\ C R D ) -> B R D ) ) ) ) $=
      ( vx vy vz wcel w3a wpo wbr wn wa wi cv wceq id breq12d binegd breq1
      birand bi2imd bi2and bilimd breq2 birimd biland wal wral df-po r3al bitr
      biimp 19.21bbi 19.21bi com12 vtocl3ga ) BAICAIDAIJAEKZBBELZMZBCELZCDELZNZ
      BDELZOZNZUSFPZVHELZMZVHGPZELZVKHPZELZNZVHVMELZOZNZOUSVABVKELZVNNZBVMELZOZ
      NZOUSVAVBCVMELZNZWAOZNZOUSVGOFGHBCDAAAVHBQZVRWCUSWHVJVAVQWBWHVIUTWHVHBVHB
      EWHRZWISTWHVOVTVPWAWHVLVSVNVHBVKEUAUBVHBVMEUAUCUDUEVKCQZWCWGUSWJWBWFVAWJV
      TWEWAWJVSVBVNWDVKCBEUFVKCVMEUAUDUGUHUEVMDQZWGVGUSWKWFVFVAWKWEVDWAVEWKWDVC
      VBVMDCEUFUHVMDBEUFUCUHUEUSVHAIVKAIVMAIJZVRUSWLVROZHUSWMHUIZFGUSWNGUIFUIZU
      SVRHAUJGAUJFAUJWOFGHAEUKVRFGHAAAULUMUNUOUPUQURUQ $.
      $( [27-Mar-97] $)
  $}

  $( A partial order relation is irreflexive. $)
  poirr $p |- ( ( R Po A /\ B e. A ) -> -. B R B ) $=
    ( wpo wcel w3a wbr wn wa wi pocl imp pm3.26d anidm df-3an anabs1 bitr2
    bitr3 sylan5b ) ACDZBAEZUAUAFZBBCGZHZUATUBIUDUCUCIUCJZTUBUDUEIABBBCKLMUAUAU
    AIZUBUANUBUFUAIUFUAUAUAOUAUAPQRS $.
    $( [27-Mar-97] $)

  $( A partial order relation is a transitive relation. $)
  potr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wpo wcel w3a wa wbr wn wi pocl imp pm3.27d ) AEFZBAGCAGDAGHZIBBEJKZBCEJCD
    EJIBDEJLZPQRSIABCDEMNO $.
    $( [27-Mar-97] $)

  $( A partial order relation has no 2-cycle loops. $)
  po2nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wpo wcel wa wbr wn poirr adantrr wi w3a potr df-3an sylan5br exp44 com34
    pm2.43d imp32 mtod ) ADEZBAFZCAFZGZGBCDHCBDHGZBBDHZUBUCUGIUDABDJKUBUCUDUFUG
    LZUBUCUDUHLUBUCUDUCUHUBUCUDUCUHUBUCUDUCMUHUEUCGABCBDNUCUDUCOPQRSTUA $.
    $( [27-Mar-97] $)

  $( A partial order relation has no 3-cycle loops. $)
  po3nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wpo wcel w3a wa wbr wn po2nr 3simpb sylan5 potr imrand df-3an syl5ib
    mtod ) AEFZBAGZCAGZDAGZHZIZBCEJZCDEJZDBEJZHZBDEJZUHIZTUAUCIUKKUDABDELUAUBUC
    MNUEUFUGIZUHIUKUIUEULUJUHABCDEOPUFUGUHQRS $.
    $( [27-Mar-97] $)

  ${
    $d x y z R $. $( ` x y z ` are dummy. $)
    $( Any relation is a partial ordering of the empty set. $)
    po0 $p |- R Po (/) $=
      ( vx vy vz c0 wpo cv wbr wn wa wi wral df-po wcel noel pm2.21i mprgbir )
      EAFBGZRAHIRCGZAHSDGZAHJRTAHKJDELCELZBEBCDEAMRENUAROPQ $.
      $( [28-Mar-97] $)
  $}

  ${
    $d x y R $.  $d x y A $.
    $( Define a strict or linear order relation. $)
    df-so $a |- ( R Or A <-> ( R Po A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $.
  $}

  ${
    $d x y R $.  $d x y A $.  $( ` x y ` are dummy. $)
    $( A strict order is a partial order. $)
    sopo $p |- ( R Or A -> R Po A ) $=
      ( vx vy wor wpo cv wbr weq w3o wral df-so pm3.26bd ) ABEABFCGZDGZBHCDIONB
      HJDAKCAKCDABLM $.
      $( [28-Mar-97] $)
  $}

  ${
    $d x y R $.  $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Subset theorem for the strict ordering predicate. $)
    soss $p |- ( A (_ B -> ( R Or B -> R Or A ) ) $=
      ( vx vy wss wpo cv wbr weq w3o wral wa wor poss wcel wi wal ssel im2and
      syl4d 19.20dv r2al 3imtr4g df-so ) ABFZBCGZDHZEHZCIDEJUIUHCIKZEBLDBLZMACG
      ZUJEALDALZMBCNACNUFUGULUKUMABCOUFUHBPZUIBPZMZUJQZERZDRUHAPZUIAPZMZUJQZERZ
      DRUKUMUFURVCDUFUQVBEUFVAUPUJUFUSUNUTUOABUHSABUISTUAUBUBUJDEBBUCUJDEAAUCUD
      TDEBCUEDEACUEUD $.
      $( [16-Mar-97] $)
  $}

  ${
    $d x y R $.  $d x y S $.  $d x y A $.  $( ` x y ` are dummy. $)
    $( Equality theorem for the strict ordering predicate. $)
    soeq1 $p |- ( R = S -> ( R Or A <-> S Or A ) ) $=
      ( vx vy wceq wpo cv wbr weq w3o wral wa wor poeq1 breq pm4.2i bi3ord
      biraldv bi2and df-so 3bitr4g ) BCFZABGZDHZEHZBIZDEJZUFUEBIZKZEALZDALZMACG
      ZUEUFCIZUHUFUECIZKZEALZDALZMABNACNUCUDUMULURABCOUCUKUQDAUCUJUPEAUCUGUNUHU
      HUIUOUEUFBCPUCUHQUFUEBCPRSSTDEABUADEACUAUB $.
      $( [16-Mar-97] $)
  $}

  $( Equality theorem for the strict ordering predicate. $)
  soeq2 $p |- ( A = B -> ( R Or A <-> R Or B ) ) $=
    ( wceq wor wss wa wi wb soss im2an eqss bi 3imtr4 bicomd ) ABDZBCEZACEZABFZ
    BAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [16-Mar-97] $)

  $( A strict order relation is irreflexive. $)
  sonr $p |- ( ( R Or A /\ B e. A ) -> -. B R B ) $=
    ( wpo wcel wbr wn wor poirr sopo sylan ) ACDBAEBBCFGACHABCIACJK $.
    $( [24-Nov-95] $)

  $( A strict order relation is a transitive relation. $)
  sotr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wpo wcel w3a wbr wa wi wor potr sopo sylan ) AEFBAGCAGDAGHBCEICDEIJBDEIKA
    ELABCDEMAENO $.
    $( [21-Jan-96] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y R $.
      $( ` x y ` are dummy. $)
    $( A strict order relation is linear (satisfies trichotomy). $)
    solin $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C \/ B = C \/ C R B ) ) $=
      ( vx vy wor wcel wa wbr wceq w3o cv weq wi breq1 cleq1 breq2 bi3ord
      bilimd cleq2 wpo wral df-so ra42 adantl sylbi com12 vtocl2ga imp ) ADGZBA
      HCAHIZBCDJZBCKZCBDJZLZULUKUPUKEMZFMZDJZEFNZURUQDJZLZOUKBURDJZBURKZURBDJZL
      ZOUKUPOEFBCAAUQBKZVBVFUKVGUSVCUTVDVAVEUQBURDPUQBURQUQBURDRSTURCKZVFUPUKVH
      VCUMVDUNVEUOURCBDRURCBUAURCBDPSTUKUQAHURAHIZVBUKADUBZVBFAUCEAUCZIVIVBOZEF
      ADUDVKVLVJVBEFAAUEUFUGUHUIUHUJ $.
      $( [21-Jan-96] $)
  $}

  $( A strict order relation has no 2-cycle loops. $)
  so2nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wpo wcel wa wbr wn wor po2nr sopo sylan ) ADEBAFCAFGBCDHCBDHGIADJABCDKADL
    M $.
    $( [21-Jan-96] $)

  $( A strict order relation has no 3-cycle loops. $)
  so3nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wpo wcel w3a wbr wn wor po3nr sopo sylan ) AEFBAGCAGDAGHBCEICDEIDBEIHJAEK
    ABCDELAEMN $.
    $( [21-Jan-96] $)

  $( A strict order relation satisfies strict trichotomy. $)
  sotric $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C <-> -. ( B = C \/ C R B ) ) ) $=
    ( wor wcel wa wceq wbr wo wn wi breq2 binegd sonr syl5bi com12 adantrr
    so2nr imnan sylibr con2d jaod w3o solin 3orass df-or bitr sylib impbid
    bicon2d ) ADEZBAFZCAFZGGZBCHZCBDIZJZBCDIZUOURUSKZUOUPUTUQULUMUPUTLUNUPULUMG
    ZUTUPBBDIZKUTVAUPVBUSBCBDMNABDOPQRUOUSUQUOUSUQGKUSUQKLABCDSUSUQTUAUBUCUOUSU
    PUQUDZUTURLZABCDUEVCUSURJVDUSUPUQUFUSURUGUHUIUJUK $.
    $( [19-Feb-96] $)

  $( Trichotomy law for strict order relation. $)
  sotrieq $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> -. ( B R C \/ C R B ) ) ) $=
    ( wor wcel wa wceq wbr wn wo wi breq2 binegd sonr syl5bi syl5bir im2and
    com12 anandis sotric bicon2d biimpar ord con1d exp imp3a impbid ioran
    syl6bbr ) ADEZBAFZCAFZGGZBCHZBCDIZJZCBDIZJZGZUPURKJUNUOUTUKULUMUOUTLUOUKULG
    ZUKUMGZGUTUOVAUQVBUSUOBBDIZJUQVAUOVCUPBCBDMNABDOPUOUSCCDIZJVBUOURVDBCCDMNAC
    DOQRSTUNUQUSUOUNUQUSUOLUNUQGZUOURVEUOURUNUOURKZUQUNUPVFABCDUAUBUCUDUEUFUGUH
    UPURUIUJ $.
    $( [9-Apr-96] $)

  $( Trichotomy law for strict order relation. $)
  sotrieq2 $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> ( -. B R C /\ -. C R B ) ) ) $=
    ( wor wcel wa wceq wbr wo wn sotrieq ioran syl6bb ) ADEBAFCAFGGBCHBCDIZCBDI
    ZJKOKPKGABCDLOPMN $.
    $( [5-May-99] $)

  ${
    $d x y z R $.  $d x y z A $.
    itlso.1 $e |- ( x e. A -> -. x R x ) $.
    itlso.2 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                  ( ( x R y /\ y R z ) -> x R z ) ) $.
    itlso.3 $e |- ( ( x e. A /\ y e. A ) -> ( x R y \/ x = y \/ y R x ) ) $.
    $( A irreflexive, transitive, linear relation is a strict ordering. $)
    itlso $p |- R Or A $=
      ( wor wpo cv wbr weq w3o wral wa wn wi wcel w3a adantr 3adant3 jca rgen3
      df-po mpbir rgen2 pm3.2i df-so ) DEIDEJZAKZBKZELZABMULUKELNZBDOADOZPUJUOU
      JUKUKELQZUMULCKZELPUKUQELRZPZCDOBDOADOUSABCDUKDSZULDSZUQDSZTUPURUTVAUPVBU
      TUPVAFUAUBGUCUDABCDEUEUFUNABDHUGUHABDEUIUF $.
      $( [21-Jan-96] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    so.1 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                  ( ( x R y <-> -. ( x = y \/ y R x ) ) /\
                 ( ( x R y /\ y R z ) -> x R z ) ) ) $.
    $( Deduce strict ordering from its properties. $)
    so $p |- R Or A $=
      ( cv wcel wbr wn wa weq wo cleqid orc ax-mp wb wi eleq1 biland cleq2
      breq1 bi2ord breq2 binegd bi2bid bi2imd w3a 3anass anidm bilan bitr2
      pm4.2i bi3and birimd pm3.26d chv sylbi bicon2d mpbii anidms pm3.27d w3o
      biimprd 3orass df-or bitr sylibr itlso ) ABCDEAGZDHZVJVJEIZJZVKVKKZAALZVL
      MZVMVOVPVJNVOVLOPVKBGZDHZKZABLZVQVJEIZMZVJVQEIZJZQZRVNVPVMQZRBABALZVSVNWE
      WFWGVRVKVKVQVJDSTWGWBVPWDVMWGVTVOWAVLVQVJVJUAVQVJVJEUBUCWGWCVLVQVJVJEUDUE
      UFUGVSWCWBVSVKVRVRUHZWCWBJQZWHVKVRVRKZKVSVKVRVRUIWJVRVKVRUJUKULVKVRCGZDHZ
      UHZWIRWHWIRCBCBLZWMWHWIWNVKVKVRVRWLVRWNVKUMWNVRUMWKVQDSUNUOWMWIWCVQWKEIKV
      JWKEIRZFUPUQURUSZUQUTVAWMWIWOFVBVSWDWBRZWCVTWAVCZVSWBWDWPVDWRWCWBMWQWCVTW
      AVEWCWBVFVGVHVI $.
      $( [29-Jan-96] $)
  $}

  ${
    $d x y R $. $( ` x y ` are dummy. $)
    $( Any relation is a strict ordering of the empty set. $)
    so0 $p |- R Or (/) $=
      ( vx vy c0 wor cv wbr weq w3o wral wpo df-so po0 mpbiran wcel noel
      pm2.21i mprgbir ) DAEZBFZCFZAGBCHUATAGICDJZBDSDAKUBBDJBCDALAMNTDOUBTPQR
      $.
      $( [16-Mar-97] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Define the supremum of class ` B ` .  It is meaningful when ` R `
       is a relation that strictly orders ` A ` and when the supremum
       exists.  For example, ` R ` could be 'less than', ` A ` could be the
       set of real numbers, and ` B ` could be the set of all positive
       reals whose square is less than 2; in this case the supremum is defined
       as the square root of 2 per ~ sqrval . $)
    df-sup $a |- sup ( B , A , R ) = U. { x e. A | ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) } $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
        $( ` x y z ` are dummy. $)
    $( Equality theorem for supremum. $)
    supeq1 $p |- ( B = C -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup raleq rexeq
      bilimd biraldv bi2and birabsdv unieqd df-sup 3eqtr4g ) BCHZEIZFIZDJKZFBLZ
      UJUIDJZUJGIDJZGBMZNZFALZOZEAPZQUKFCLZUMUNGCMZNZFALZOZEAPZQBADRCADRUHUSVEU
      HURVDEAUHULUTUQVCUKFBCSUHUPVBFAUHUOVAUMUNGBCTUAUBUCUDUEEFGABDUFEFGACDUFUG
      $.
      $( [22-May-99] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $( `w` is dummy. $)
    supmo.1 $e |- R Or A $.
    $( Any class ` B ` has at most one supremum in ` A ` (where ` R ` is
       interpreted as 'less than'). $)
    supmo $p |- E* x ( x e. A /\ ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( vw cv wcel wbr wn wral wrex wi wa wmo weq wal eleq1 breq1 binegd
      biraldv breq2 birimd bi2and mo4 birexdv bi2imd rcla4v imp cbvralv ralnex
      bitr bicon2 syl6ib con2d exp a1i com4t imp42 adantrl com4l imp45 adantll
      jca wb wor sotrieq2 mpan adantrr adantlr mpbird ax-gen mpgbir ) AIZDJZVPB
      IZFKZLZBEMZVRVPFKZVRCIZFKZCENZOZBDMZPZPZAQWIHIZDJZWJVRFKZLZBEMZVRWJFKZWEO
      ZBDMZPZPZPZAHRZOZHSAWIWSAHXAVQWKWHWRVPWJDTXAWAWNWGWQXAVTWMBEXAVSWLVPWJVRF
      UAUBUCXAWFWPBDXAWBWOWEVPWJVRFUDUEUCUFUFUGXBHWTXAVPWJFKZLZWJVPFKZLZPZWTXDX
      FWIWRXDWKWIWQXDWNVQWAWGWQXDWGWQVQWAXDWQVQWAXDOZOOWGWQVQXHWQVQPZXCWAXIXCVP
      WCFKZCENZWALWQVQXCXKOZWPXLBVPDBARZWOXCWEXKVRVPWJFUAXMWDXJCEVRVPWCFUAUHUIU
      JUKWAXKWAXJLZCEMXKLVTXNBCEBCRZVSXJVRWCVPFUDUBULXJCEUMUNUOUPUQURUSUTVAVBVB
      WHWSXFVQWGWSXFWAWGWKWNWQXFWQWGWKWNXFWGWKWNXFOZOOWQWGWKXPWGWKPZXEWNXQXEWJW
      CFKZCENZWNLWGWKXEXSOZWFXTBWJDBHRZWBXEWEXSVRWJVPFUAYAWDXRCEVRWJWCFUAUHUIUJ
      UKWNXSWNXRLZCEMXSLWMYBBCEXOWLXRVRWCWJFUDUBULXRCEUMUNUOUPUQURUSVCVDVEVEVFV
      QWSXAXGVGZWHVQWKYCWRDFVHVQWKPYCGDVPWJFVIVJVKVLVMVNVO $.
      $( [5-May-99] $)

    $( The supremum is a set. $)
    supex $p |- sup ( B , A , R ) e. V $=
      ( vx vy vz csup cv wcel wbr wn wral wrex wi wa cab cuni cvv crab df-sup
      df-rab unieqi eqtr wmo supmo moabex ax-mp uniex eqeltr ) BACHZEIZAJULFIZC
      KLFBMUMULCKUMGICKGBNOFAMPZPZEQZRZSUKUNEATZRUQEFGABCUAURUPUNEAUBUCUDUPUOEU
      EUPSJEFGABCDUFUOEUGUHUIUJ $.
      $( [22-May-99] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    sup.1 $e |- R Or A $.
    sup.2 $e |- E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $.

    $( The supremum is unique. $)
    supeu $p |- E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $=
      ( cv wbr wn wral wrex wi wa wreu wcel wmo supmo pm3.2i reu5 mpbir ) AIZBI
      ZFJKBELUDUCFJUDCIFJCEMNBDLOZADPUEADMZUCDQUEOARZOUFUGHABCDEFGSTUEADUAUB $.
      $( [23-May-99] $)

    $( The supremum belongs to its class (closure law). $)
    supcl $p |- sup ( B , A , R ) e. A $=
      ( csup cv wbr wn wral wrex wi wa crab cuni df-sup wreu wcel supeu reucl
      ax-mp eqeltr ) EDFIAJZBJZFKLBEMUGUFFKUGCJFKCENOBDMPZADQRZDABCDEFSUHADTUID
      UAABCDEFGHUBUHADUCUDUE $.
      $( [23-May-99] $)

    $( The supremum is an upper bound. $)
    supub $p |- ( C e. B -> -. sup ( B , A , R ) R C ) $=
      ( vw csup cv wbr wn wceq breq2 binegd wral wcel wi wrex wa crab cuni
      df-sup cleqcomi wreu wb supcl supeu breq1 biraldv birimd bi2and reuunis
      mp2an mpbir pm3.26i weq rcla4v ax-mp vtoclga ) EDGKZJLZGMZNZVCFGMZNJFEVDF
      OVEVGVDFVCGPQVCBLZGMZNZBERZVDESVFTVKVHVCGMZVHCLGMCEUAZTZBDRZVKVOUBZALZVHG
      MZNZBERZVHVQGMZVMTZBDRZUBZADUCUDZVCOZVCWEABCDEGUEUFVCDSWDADUGVPWFUHABCDEG
      HIUIABCDEGHIUJWDVPADVCVQVCOZVTVKWCVOWGVSVJBEWGVRVIVQVCVHGUKQULWGWBVNBDWGW
      AVLVMVQVCVHGPUMULUNUOUPUQURVJVFBVDEBJUSVIVEVHVDVCGPQUTVAVB $.
      $( [23-May-99] $)

    ${
      $d z C $.
      $( The supremum is the least upper bound. $)
      suplub $p |- ( ( C e. A /\ C R sup ( B , A , R ) ) -> E. z e. B C R z ) $=
        ( vw wcel csup wbr cv wrex wi wceq breq1 birexdv bi2imd wral wn wa
        crab cuni df-sup cleqcomi wreu wb supcl supeu binegd biraldv breq2
        birimd bi2and reuunis mp2an mpbir pm3.27i weq rcla4v ax-mp vtoclga imp
        ) FDKFEDGLZGMZFCNZGMZCEOZJNZVFGMZVKVHGMZCEOZPZVGVJPJFDVKFQZVLVGVNVJVKFV
        FGRVPVMVICEVKFVHGRSTBNZVFGMZVQVHGMZCEOZPZBDUAZVKDKVOPVFVQGMZUBZBEUAZWBW
        EWBUCZANZVQGMZUBZBEUAZVQWGGMZVTPZBDUAZUCZADUDUEZVFQZVFWOABCDEGUFUGVFDKW
        NADUHWFWPUIABCDEGHIUJABCDEGHIUKWNWFADVFWGVFQZWJWEWMWBWQWIWDBEWQWHWCWGVF
        VQGRULUMWQWLWABDWQWKVRVTWGVFVQGUNUOUMUPUQURUSUTWAVOBVKDBJVAZVRVLVTVNVQV
        KVFGRWRVSVMCEVQVKVHGRSTVBVCVDVE $.
        $( [23-May-99] $)

      $( An upper bound is not less than the supremum. $)
      supnub $p |- ( ( C e. A /\ A. z e. B -. C R z ) ->
                   -. C R sup ( B , A , R ) ) $=
        ( wcel cv wbr wn wral csup wrex suplub exp dfrex2 syl6ib con2d imp ) FD
        JZFCKGLZMCENZFEDGOGLZMUCUFUEUCUFUDCEPZUEMUCUFUGABCDEFGHIQRUDCESTUAUB $.
        $( [23-May-99] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.  $( ` x y z ` are dummy. $)
    supsn.1 $e |- R Or A $.
    $( The supremum of a singleton. $)
    supsn $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( vx vy vz wcel cv wbr wn csn wral wrex wi wa crab cuni csup wceq elsni
      breq2 binegd wor sonr mpan syl5bir syl com12 r19.21aiv rcla4ev snidg
      sylan exp a1d jca wreu wb wmo breq1 biraldv birimd bi2and mpand supmo
      jctir reu5 sylibr reuunis mpbid df-sup syl5eq ) BAHZEIZFIZCJZKZFBLZMZVOVN
      CJZVOGIZCJZGVRNZOZFAMZPZEAQRZBVRACSVMBVOCJZKZFVRMZVOBCJZWCOZFAMZPZWGBTZVM
      WJWMVMWIFVRVOVRHZVMWIWPVOBTZVMWIOVOBUAWQWIBBCJZKZVMWQWHWRVOBBCUBUCACUDVMW
      SDABCUEUFUGUHUIUJVMWLFAVMWLVOAHVMWKWCBVRHWKWCVMWBWKGBVRWABVOCUBUKBAULUMUN
      UOUJUPZVMWFEAUQZWNWOURVMWFEANZVNAHWFPEUSZPXAVMXBXCVMWNXBWTWFWNEBAVNBTZVSW
      JWEWMXDVQWIFVRXDVPWHVNBVOCUTUCVAXDWDWLFAXDVTWKWCVNBVOCUBVBVAVCZUKVDEFGAVR
      CDVEVFWFEAVGVHWFWNEABXEVIVDVJEFGAVRCVKVL $.
      $( [23-May-99] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    $( Define a founded relation.  For alternate definitions, see ~ dffr2 and
       ~ dffr3 . $)
    df-fr $a |- ( R Fr A <-> A. x ( ( x (_ A /\ -. x = (/) ) ->
                E. y e. x A. z e. x -. z R y ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` z ` is dummy. $)
    fri.1 $e |- B e. V $.
    $( Property of founded relation (one direction of definition). $)
    fri $p |- ( ( R Fr A /\ ( B (_ A /\ -. B = (/) ) ) ->
                E. x e. B A. y e. B -. y R x ) $=
      ( vz wfr wss c0 wceq wn wa cv wbr wral wrex wi wal df-fr sseq1 cleq1
      binegd bi2and raleq rexeqd bi2imd cla4v sylbi imp ) CEHZDCIZDJKZLZMZBNANE
      OLZBDPZADQZUKGNZCIZUSJKZLZMZUPBUSPZAUSQZRZGSUOURRZGABCETVFVGGDFUSDKZVCUOV
      EURVHUTULVBUNUSDCUAVHVAUMUSDJUBUCUDVDUQAUSDUPBUSDUEUFUGUHUIUJ $.
      $( [18-Mar-97] $)
  $}

  ${
    $d x y z w v R $.  $d x w v A $.
    $( Alternate definition of founded relation.  Similar to Definition 6.21 of
       [TakeutiZaring] p. 30. $)
    dffr2 $p |- ( R Fr A <-> A. x ( ( x (_ A /\ -. x = (/) ) ->
                E. y e. x ( x i^i { z | z R y } ) = (/) ) ) $=
      ( vw vv wfr cv wss c0 wceq wn wa wbr wral wrex wi wal cab cin df-fr wcel
      disj visset breq1 elab bineg biral bitr birex weq breq2 binegd biraldv
      cbvrexv bilim bial bitr4 ) DEHAIZDJUTKLMNZFIZGIZEOZMZFUTPZGUTQZRZASVAUTCI
      ZBIZEOZCTZUAKLZBUTQZRZASAGFDEUBVOVHAVNVGVAVNVBVJEOZMZFUTPZBUTQVGVMVRBUTVM
      VBVLUCZMZFUTPVRFUTVLUDVTVQFUTVSVPVKVPCVBFUEVIVBVJEUFUGUHUIUJUKVRVFBGUTBGU
      LZVQVEFUTWAVPVDVJVCVBEUMUNUOUPUJUQURUS $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` z ` is dummy. $)
    frc.1 $e |- B e. V $.
    $( Property of founded relation (one direction of definition using class
       variables). $)
    frc $p |- ( R Fr A -> ( ( B (_ A /\ -. B = (/) ) ->
                E. x e. B ( B i^i { y | y R x } ) = (/) ) ) $=
      ( vz wfr cv wss c0 wceq wn wa wbr cab cin wrex wi wal dffr2 sseq1 cleq1
      binegd bi2and ineq1 cleq1d rexeqd bi2imd cla4v sylbi ) CEHGIZCJZULKLZMZNZ
      ULBIAIEOBPZQZKLZAULRZSZGTDCJZDKLZMZNZDUQQZKLZADRZSZGABCEUAVAVIGDFULDLZUPV
      EUTVHVJUMVBUOVDULDCUBVJUNVCULDKUCUDUEUSVGAULDVJURVFKULDUQUFUGUHUIUJUK $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Subset theorem for the founded predicate.  Exercise 1 of
       [TakeutiZaring] p. 31. $)
    frss $p |- ( A (_ B -> ( R Fr B -> R Fr A ) ) $=
      ( vx vz vy wss cv c0 wceq wn wa wbr cab cin wrex wi wal wfr sstr com12
      imrand syl4d 19.20dv dffr2 3imtr4g ) ABGZDHZBGZUHIJKZLZUHEHFHCMENOIJFUHPZ
      QZDRUHAGZUJLZULQZDRBCSACSUGUMUPDUGUOUKULUGUNUIUJUNUGUIUHABTUAUBUCUDDFEBCU
      EDFEACUEUF $.
      $( [3-Apr-94] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Equality theorem for the founded predicate. $)
    freq1 $p |- ( R = S -> ( R Fr A <-> S Fr A ) ) $=
      ( vx vz vy wceq cv wss c0 wn wa wbr wral wrex wi wal wfr breq binegd
      biraldv birexdv bilimd bialdv df-fr 3bitr4g ) BCGZDHZAIUHJGKLZEHZFHZBMZKZ
      EUHNZFUHOZPZDQUIUJUKCMZKZEUHNZFUHOZPZDQABRACRUGUPVADUGUOUTUIUGUNUSFUHUGUM
      UREUHUGULUQUJUKBCSTUAUBUCUDDFEABUEDFEACUEUF $.
      $( [9-Mar-97] $)
  $}

  $( Equality theorem for the founded predicate. $)
  freq2 $p |- ( A = B -> ( R Fr A <-> R Fr B ) ) $=
    ( wceq wfr wss wa wi wb frss im2an eqss bi 3imtr4 bicomd ) ABDZBCEZACEZABFZ
    BAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [3-Apr-94] $)

  ${
    $d x y z R $.  $d y z A $.  $( ` y z ` are dummy. $)
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30. $)
    frirr $p |- ( ( R Fr A /\ x e. A ) -> -. x R x ) $=
      ( vz vy wfr cv wcel wbr wn csn cab cin c0 wceq wrex visset snnz wss wi
      snex frc exp3a snss syl5ib mpii weq elsn breq2 biabdv ineq2d cleq1d wa
      breq1 elab biimpr snid jctil elin sylibr noel2 syl con2i syl6bi sylbi
      r19.23aiv syl6 imp ) BCFZAGZBHZVJVJCIZJZVIVKVJKZDGZEGZCIZDLZMZNOZEVNPZVMV
      IVKVNNOJZWAVJAQZRVIVNBSZWBWATVKVIWDWBWAEDBVNCVJUAUBUCVJBWCUDUEUFVTVMEVNVP
      VNHEAUGZVTVMTEVJUHWEVTVNVOVJCIZDLZMZNOZVMWEVSWHNWEVRWGVNWEVQWFDVPVJVOCUIU
      JUKULVLWIVLVJWHHZWIJVLVJVNHZVJWGHZUMWJVLWLWKWLVLWFVLDVJWCVOVJVJCUNUOUPVJW
      CUQURVJVNWGUSUTWHVJVAVBVCVDVEVFVGVH $.
      $( [2-Jan-94] $)
  $}

  ${
    $d z w R $.  $d z w x $.  $d z w y $.
    $d z w A $.  $( ` z w ` are dummy. $)
    $( A founded relation has no 2-cycle loops.  Special case of
       Proposition 6.23 of [TakeutiZaring] p. 30. $)
    fr2nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x R y /\ y R x ) ) $=
      ( vz vw wfr cv wcel wa wbr wn cpr wss c0 wceq visset prnz cab cin wrex
      zfpair frc wi weq wo breq2 biabdv ineq2d cleq1d binegd brab1 pri2 inelcm
      mpan sylbi syl5bir com12 pri1 jaao elpr syl5ib con3i expi r19.23aiv syl6
      mpan2i prss ancom imp ) CDGZAHZCIZBHZCIZJZVLVNDKZVNVLDKZJZLZVKVOVMJZVTVPV
      KVNVLMZCNZVTWAVKWCWBOPLZVTVNVLBQZRVKWCWDJWBEHZFHZDKZESZTZOPZFWBUAVTFECWBD
      BAUBUCWKVTFWBWGWBIZWKVTVSWLWKLZUDVSFBUEZFAUEZUFWMWLVQWNWMVRWOWNVQWMWNWMWB
      WFVNDKZESZTZOPZLZVQWNWKWSWNWJWROWNWIWQWBWNWHWPEWGVNWFDUGUHUIUJUKVQVLWQIZW
      TABEDULVLWBIXAWTVNVLAQZUMVLWBWQUNUOUPUQURWOVRWMWOWMWBWFVLDKZESZTZOPZLZVRW
      OWKXFWOWJXEOWOWIXDWBWOWHXCEWGVLWFDUGUHUIUJUKVRVNXDIZXGBAEDULVNWBIXHXGVNVL
      WEUSVNWBXDUNUOUPUQURUTWGVNVLFQVAVBVCVDVEVFVGVNVLCWEXBVHVBVMVOVIVBVJ $.
      $( [30-May-94] $)
  $}

  ${
    $d w v x $.  $d w v y $.  $d w v z $.  $d w v R $.
    $d w v A $.  $( ` w v ` are dummy. $)
    $( A founded relation has no 3-cycle loops.  Special case of
       Proposition 6.23 of [TakeutiZaring] p. 30. $)
    fr3nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x R y /\ y R z /\ z R x ) ) $=
      ( vw vv wfr cv wcel w3a wbr wn ctp wss c0 wceq visset tpnz wa cab cin
      wrex tpex frc wi weq w3o 3jao breq2 biabdv ineq2d cleq1d binegd brab1
      tpi3 inelcm mpan sylbi syl5bir com12 tpi1 tpi2 syl3an eltp syl5ib con3i
      expi r19.23aiv syl6 mpan2i tpss 3anrot imp ) DEHZAIZDJZBIZDJZCIZDJZKZVPVR
      ELZVRVTELZVTVPELZKZMZVOVSWAVQKZWGWBVOVRVTVPNZDOZWGWHVOWJWIPQMZWGVRVTVPBRZ
      SVOWJWKTWIFIZGIZELZFUAZUBZPQZGWIUCWGGFDWIEVRVTVPUDUEWRWGGWIWNWIJZWRWGWFWS
      WRMZUFWFGBUGZGCUGZGAUGZUHZWTWSXAWTUFXBWTUFXCWTUFXDWTUFWCXAWTXBXCUIWDWEXAW
      CWTXAWTWIWMVRELZFUAZUBZPQZMZWCXAWRXHXAWQXGPXAWPXFWIXAWOXEFWNVRWMEUJUKULUM
      UNWCVPXFJZXIABFEUOVPWIJXJXIVRVTVPARZUPVPWIXFUQURUSUTVAXBWDWTXBWTWIWMVTELZ
      FUAZUBZPQZMZWDXBWRXOXBWQXNPXBWPXMWIXBWOXLFWNVTWMEUJUKULUMUNWDVRXMJZXPBCFE
      UOVRWIJXQXPVRVTVPWLVBVRWIXMUQURUSUTVAXCWEWTXCWTWIWMVPELZFUAZUBZPQZMZWEXCW
      RYAXCWQXTPXCWPXSWIXCWOXRFWNVPWMEUJUKULUMUNWEVTXSJZYBCAFEUOVTWIJYCYBVRVTVP
      CRZVCVTWIXSUQURUSUTVAVDWNVRVTVPGRVEVFVGVHVIVJVKVRVTVPDWLYDXKVLVFVQVSWAVMV
      FVN $.
      $( [10-Apr-94] $)
  $}

  ${
    $d x y z R $.  $( ` x y z ` are dummy. $)
    $( Any relation is founded on the empty set. $)
    fr0 $p |- R Fr (/) $=
      ( vx vz vy c0 wfr cv wss wceq wn wa wbr cab cin wrex wi dffr2 ss0 iman
      mpbi pm2.21i mpgbir ) EAFBGZEHZUCEIZJKZUCCGDGALCMNEIDUCOZPBBDCEAQUFUGUDUE
      PUFJUCRUDUESTUAUB $.
      $( [17-Sep-93] $)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Irreflexivitiy of the epsilon relation:  a class founded by epsilon is
       not a member of itself. $)
    efrirr $p |- ( E Fr A  -> -. A e. A ) $=
      ( vx cep wfr wcel wn cv wel wi wceq freq2 eleq1 eleq2 bitrd binegd
      bi2imd wbr frirr exp epel bineg syl6ib pm2.01d vtoclg com12 ) ACDZAAEZUGU
      FUGFZBGZCDZBBHZFZIUFUHIBAAUIAJZUJUFULUHUIACKUMUKUGUMUKAUIEUGUIAUILUIAAMNO
      PUJUKUJUKUIUICQZFZULUJUKUOBUICRSUNUKBBTUAUBUCUDUEUC $.
      $( [18-Apr-94] $)
  $}

  $( A set founded by epsilon contains no 2-cycle loops. $)
  efrn2lp $p |- ( ( E Fr A /\ ( x e. A /\ y e. A ) ) ->
                -. ( x e. y /\ y e. x ) ) $=
    ( cep wfr cv wcel wa wbr wn wel fr2nr epel bi2an bineg sylib ) CDEAFZCGBFZC
    GHHQRDIZRQDIZHZJABKZBAKZHZJABCDLUAUDSUBTUCABMBAMNOP $.
    $( [19-Apr-94] $)

  $( A set founded by epsilon contains no 3-cycle loops. $)
  epne3 $p |- ( ( E Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x e. y /\ y e. z /\ z e. x ) ) $=
    ( cep wfr cv wcel w3a wa wbr wn wel fr3nr epel bi3an bineg sylib ) DEFAGZDH
    BGZDHCGZDHIJSTEKZTUAEKZUASEKZIZLABMZBCMZCAMZIZLABCDENUEUIUBUFUCUGUDUHABOBCO
    CAOPQR $.
    $( [19-Apr-94] $)

  $( Similar to Theorem 7.2 of [TakeutiZaring] p. 35, of  except that the
     Axiom of Regularity is not required due to antecedent ` E Fr A ` . $)
  tz7.2 $p |- (  ( ( Tr A /\ E Fr A ) /\ B e. A ) ->
             ( B (_ A /\ -. B = A ) ) $=
    ( wtr cep wfr wa wcel wss wceq wn wi trss eleq1 binegd efrirr syl5bir
    com12 con2d im2an jcab sylibr imp ) ACZADEZFZBAGZBAHZBAIZJZFZUEUFUGKZUFUIKZ
    FUFUJKUCUKUDULABLUDUHUFUHUDUFJZUHUMAAGZJUDUHUFUNBAAMNAOPQRSUFUGUITUAUB $.
    $( [4-May-94] $)

  ${
    $d x y z $.  $d x z A $.
    $( A simpler way of saying that the epsilon relation is founded. $)
    dfepfr $p |- ( E Fr A <-> A. x ( ( x (_ A /\ -. x = (/) ) ->
                  E. y e. x ( x i^i y ) = (/) ) ) $=
      ( vz cep wfr cv wss c0 wceq wn wa wbr cab cin wrex wi wal dffr2 wel epel
      biabi abid2 eqtr ineq2i cleq1i birex bilim bial bitr ) CEFAGZCHUKIJKLZUKD
      GBGZEMZDNZOZIJZBUKPZQZARULUKUMOZIJZBUKPZQZARABDCESUSVCAURVBULUQVABUKUPUTI
      UOUMUKUODBTZDNUMUNVDDDBUAUBDUMUCUDUEUFUGUHUIUJ $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    epfrc.1 $e |- B e. V $.
    $( A subset of an epsilon-founded class has a minimal element. $)
    epfrc $p |- ( ( E Fr A /\ ( B (_ A /\ -. B = (/) ) ) ->
                  E. x e. B ( B i^i x ) = (/) ) $=
      ( vy cep wfr wss c0 wceq wn wa cv wbr cab cin wrex frc imp wel epel
      biabi abid2 eqtr2 ineq2i cleq1i birex sylibr ) BFGZCBHCIJKLZLCEMAMZFNZEOZ
      PZIJZACQZCUKPZIJZACQUIUJUPAEBCFDRSURUOACUQUNIUKUMCUMEATZEOUKULUSEEAUAUBEU
      KUCUDUEUFUGUH $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  $( Define a well-ordering.  For an alternate definition see ~ dfwe2 . $)
  df-we $a |- ( R We A <-> ( R Fr A /\ R Or A ) ) $.

  ${
    $d x y z R $.  $d x y z A $.  $( ` z ` is dummy. $)
    $( Alternate definition of well-ordering.  Definition 6.24(2) of
       [TakeutiZaring] p. 30. $)
    dfwe2 $p |- ( R We A <-> ( R Fr A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $=
      ( vz wwe wfr wor wa cv wbr weq w3o wral df-we wcel wi wal solin exp
      19.21aivv r2al sylibr imlan wpo wn w3a frirr adantrr 3simpa sylan5 a1d
      wo fr2nr breq2 biimprd imland com12 imp nsyl imnan fr3nr df-3an bineg
      sylib jcad ioran syl6ibr syl4d 3orass orcom df-or 3bitr syl5ib a2d
      19.20dv r3al 3imtr4g ralidm cleq2 breq1 bi3ord cbvralv biral bitr3 df-po
      ancrd df-so imdistani impbi bitr ) CDFCDGZCDHZIZWLAJZBJZDKZABLZWPWODKZMZB
      CNZACNZIZCDOWNXCWMXBWLWMWOCPZWPCPZIZWTQZBRARXBWMXGABWMXFWTCWOWPDSTUAWTABC
      CUBUCUDWLXBWMWLXBCDUEZXBIWMWLXBXHWLWOEJZDKZAELZXIWODKZMZECNZBCNZACNZWOWOD
      KUFZWQWPXIDKZIZXJQZIZECNBCNACNZXBXHWLXDXEXICPZUGZXMQZERZBRZARYDYAQZERZBRZ
      ARXPYBWLYGYJAWLYFYIBWLYEYHEWLYDXMYAWLYDXMYAQWLYDIZXMXQXTYKXQXMWLXFXQYDWLX
      DXQXEACDUHUIXDXEYCUJZUKULYKXKXLUMZUFZXJQZXTXMYKXSYNXJYKXSXKUFZXLUFZIYNYKX
      SYPYQYKXSXKIZUFXSYPQYKWQWSIZYRWLXFYSUFYDABCDUNYLUKXSXKYSXKXSYSXKXRWSWQXKW
      SXRWOXIWPDUOUPUQURUSUTXSXKVAUCYKXSXLIZUFZXSYQQYKWQXRXLUGZUFUVAABECDVBUVBY
      TWQXRXLVCVDVEXSXLVAUCVFXKXLVGVHVIXMXJYMUMYMXJUMYOXJXKXLVJXJYMVKYMXJVLVMVN
      VFTVOVPVPVPXMABECCCVQYAABECCCVQVRXAXOACXAXABCNXOWTBCVSXAXNBCWTXMBECBELWQX
      JWRXKWSXLWPXIWODUOWPXIWOVTWPXIWODWAWBWCWDWEWDABECDWFVRWGABCDWHVHWIWJWK $.
      $( [16-Mar-97] $)
  $}

  $( Subset theorem for the well-ordering predicate.  Exercise 4 of
     [TakeutiZaring] p. 31. $)
  wess $p |- ( A (_ B -> ( R We B -> R We A ) ) $=
    ( wss wfr wor wa wwe frss soss im2and df-we 3imtr4g ) ABDZBCEZBCFZGACEZACFZ
    GBCHACHNOQPRABCIABCJKBCLACLM $.
    $( [19-Apr-94] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq1 $p |- ( R = S -> ( R We A <-> S We A ) ) $=
    ( wceq wfr wor wa wwe freq1 soeq1 bi2and df-we 3bitr4g ) BCDZABEZABFZGACEZA
    CFZGABHACHNOQPRABCIABCJKABLACLM $.
    $( [9-Mar-97] $)

  $( Equality theorem for the well-ordering predicate. $)
  weeq2 $p |- ( A = B -> ( R We A <-> R We B ) ) $=
    ( wceq wfr wor wa wwe freq2 soeq2 bi2and df-we 3bitr4g ) ABDZACEZACFZGBCEZB
    CFZGACHBCHNOQPRABCIABCJKACLBCLM $.
    $( [3-Apr-94] $)

  $( A well-ordering is founded. $)
  wefr $p |- ( R We A -> R Fr A ) $=
    ( wwe wfr wor df-we pm3.26bd ) ABCABDABEABFG $.
    $( [22-Apr-94] $)

  $( A well-ordering is a strict ordering. $)
  weso $p |- ( R We A -> R Or A ) $=
    ( wwe wfr wor df-we pm3.27bd ) ABCABDABEABFG $.
    $( [16-Mar-97] $)

  $( The elements of an epsilon well-ordering are comparable. $)
  wecmpep $p |- ( ( E We A /\ ( x e. A /\ y e. A ) ) ->
                 ( x e. y \/ x = y \/ y e. x ) ) $=
    ( cep wor cv wcel wa wel weq w3o wwe wbr solin epel pm4.2 bi3or sylib weso
    sylan ) CDEZAFZCGBFZCGHZABIZABJZBAIZKZCDLUAUDHUBUCDMZUFUCUBDMZKUHCUBUCDNUIU
    EUFUFUJUGABOUFPBAOQRCDST $.
    $( [17-May-94] $)

  $( An epsilon well-ordering is a transitive relation. $)
  wetrep $p |- ( ( E We A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
             ( ( x e. y /\ y e. z ) -> x e. z ) ) $=
    ( cep wwe cv wcel w3a wa wbr wel wor wi sotr weso sylan epel bi2an 3imtr3g
    ) DEFZAGZDHBGZDHCGZDHIZJUBUCEKZUCUDEKZJZUBUDEKZABLZBCLZJACLDEMUEUHUINUADUBU
    CUDEODEPQUFUJUGUKABRBCRSACRT $.
    $( [22-Apr-94] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( 'y' and 'z' are dummy. $)
    $( A non-empty (possibly proper) subclass of a class well-ordered by ` E `
       has a minimal element.  Special case of Proposition 6.26 of
       [TakeutiZaring] p. 31. $)
    wefrc $p |- ( ( E We A /\ ( B (_ A /\ -. B = (/) ) ) ->
               E. x e. B ( B i^i x ) = (/) ) $=
      ( vy vz cep wwe wss c0 wceq wn cv cin wrex wi wess wcel wex wa weq ineq2
      cleq1d rcla4ev exp adantl wel inss1 wfr visset inex2 epfrc wefr sylan
      exp32 mpi elin biran anass bitr birex2 syl6ib adantr wral w3a wetrep
      exp3a df-3an 3anrot bitr3 sylan5b exp44 imp com34 imp3a syl5ib imp4a
      com23 r19.21adv dfss3 syl6ibr dfss in23 cleq2i biimp biimprd syl6
      r19.22dv syld pm2.61d 19.23adv n0 com12 imp32 ) BFGZCBHZCIJKZCALZMZIJZACN
      ZWOWNWPWTOZWOWNCFGZXACBFPXBDLZCQZDRWTWPXBXDWTDXBXDWTXBXDSZCXCMZIJZWTXDXGW
      TOXBXDXGWTWSXGAXCCADTWRXFIWQXCCUAUBUCUDUEXEXGKZADUFZXFWQMZIJZSZACNZWTXBXH
      XMOXDXBXHXKAXFNZXMXBXFCHZXHXNOCXCUGXBXOXHXNCFUHXOXHSXNXBACXFXCCDUIUJUKCFU
      LUMUNUOXKXLAXFCWQXFQZXKSWQCQZXISZXKSXQXLSXPXRXKWQCXCUPUQXQXIXKURUSUTVAVBX
      EXLWSACXEXQXIXKWSXEXQXIXKWSOZXEXRWRXCHZXSXEXREDUFZEWRVCXTXEXRYAEWRXEELZWR
      QZXRYAXEYCXQXIYAXEYBCQZEAUFZSXQXIYAOZOZYCXEYDYEYGXEYDXQYEYFXBXDYDXQYEYFOZ
      OOXBXDYDXQYHXBYDXQXDVDZYHXDYDSXQSZXBYISYEXIYAEADCVEVFYJXDYDXQVDYIXDYDXQVG
      XDYDXQVHVIVJVKVLVMVNYBCWQUPVOVPVQVREWRXCVSVTXTWSXKXTWRXJIXTWRXJJZXTWRWRXC
      MZJYKWRXCWAYLXJWRCWQXCWBWCUSWDUBWEWFVFVPWGWHWIUDWJDCWKVOWFWLWM $.
      $( [18-Feb-04] $) $( [17-Feb-04] $)
  $}

  $( Any relation is a well-ordering of the empty set. $)
  we0 $p |- R We (/) $=
    ( c0 wwe wfr wor wa fr0 so0 pm3.2i df-we mpbir ) BACBADZBAEZFLMAGAHIBAJK $.
    $( [16-Mar-97] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` z ` is dummy. $)
    wereu.1 $e |- B e. V $.
    $( A subset of a well-ordered set has a unique minimal element. $)
    wereu $p |- ( ( R We A /\ ( B (_ A /\ -. B = (/) ) ) ->
                E! x e. B A. y e. B -. y R x ) $=
      ( vz wwe wss c0 wceq wn wa cv wbr wral wrex weq wi wreu wfr fri wefr
      sylan wcel wal wo w3o wor solin weso df-3or or23 df-or 3bitr sylib ioran
      syl5ibr wess imp ancoms breq1 binegd rcla4v im2anan9r syl5 exp4b pm2.43d
      adantrr 19.21aivv r2al sylibr jca breq2 biraldv reu4 ) CEHZDCIZDJKLZMZMZB
      NZANZEOZLZBDPZADQZWFWBGNZEOZLZBDPZMZAGRZSZGDPADPZMWFADTWAWGWOCEUAVTWGVQAB
      CDEFUBCEUCUDWAWCDUEZWHDUEZMZWNSZGUFAUFWOWAWSAGVQVRWSVSVQVRMZWRWNWTWRWRWLW
      MWTWRMWCWHEOZLZWHWCEOZLZMZWMWRWLMDEHZWRXEWMSWTXFWRMZXAXCUGZLZWMXEXGXAWMXC
      UHZXIWMSZDEUIWRXJXFDWCWHEUJDEUKUDXJXAWMUGXCUGXHWMUGXKXAWMXCULXAWMXCUMXHWM
      UNUOUPXAXCUQURVRVQXFVRVQXFDCEUSUTVAUDWLWRXEWLWRXEWKWPXBWFWQXDWJXBBWCDBARW
      IXAWBWCWHEVBVCVDWEXDBWHDBGRWDXCWBWHWCEVBVCVDVEUTVAVFVGVHVIVJWNAGDDVKVLVMW
      FWKAGDWMWEWJBDWMWDWIWCWHWBEVNVCVOVPVL $.
      $( [18-Mar-97] $)
  $}


