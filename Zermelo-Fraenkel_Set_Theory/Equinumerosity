$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equinumerosity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Introduce new constant symbols. $)
  $c ~~ $. $( Equinumerosity relation ("approximately equals" symbol) $)
  $c ~<_ $. $( Dominance relation (curly less-than-or-equal) $)
  $c ~< $. $( Strict dominance relation (curly less-than) $)

  $( Extend class definition to include equinumerosity relation
     ("approximately equals" symbol) $)
  cen $a class ~~ $.
  $( Extend class definition to include dominance relation (curly
     less-than-or-equal) $)
  cdom $a class ~<_ $.
  $( Extend class definition to include strict dominance relation (curly
     less-than) $)
  csdm $a class ~< $.

  ${
    $d x y f $.  $d x y f $.
    $( Define equinumerosity relation.  Definition of Enderton,
       p. 129, except we consider ` ~~ ` to be a (proper) class rather
       than a connective.  This is acceptable because equinumerosity is
       meaningful for sets only and not for proper classes. $)
    df-en $a |- ~~ = { <. x , y >. | E. f f : x -1-1-onto-> y } $.

    $( Define dominance relation.  For an alternate definition see
       ~ dfdom2 . Compare definition of Enderton, p. 145. $)
    df-dom $a |- ~<_ = { <. x , y >. | E. f f : x -1-1-> y } $.

    $( Define strict dominance relation.  For an alternate definition see
       ~ dfsdom2 .  Definition 3 of Suppes, p. 97. $)
    df-sdom $a |- ~< = ( ~<_ \ ~~ ) $.

  $}

  ${
    $d x y f $.  $d x y f $.
    $( Equinumerosity is a relation. $)
    relen $p |- Rel ~~ $=
      ( vx vy vf cen wrel cv wf1o wex copab relopab wceq wb df-en releq ax-mp
      mpbir ) DEZAFBFCFGCHZABIZEZRABJDSKQTLABCMDSNOP $.
      $([28-Mar-98]$)

    $( Dominance is a relation. $)
    reldom $p |- Rel ~<_ $=
      ( vx vy vf cdom wrel cv wf1 wex copab relopab wceq wb df-dom releq ax-mp
      mpbir ) DEZAFBFCFGCHZABIZEZRABJDSKQTLABCMDSNOP $.
      $([28-Mar-98]$)
  $}

  $( Strict dominance is a relation. $)
  relsdom $p |- Rel ~< $=
    ( cdom wrel csdm reldom cen cdif reldif wceq wb df-sdom releq ax-mp sylibr
    ) ABZCBZDNAEFZBZOAEGCPHOQIJCPKLML $.
    $([31-Mar-98]$)

  ${
    $d f x y A $.  $d f x y B $.  $d y C $.  $( ` x y ` are dummy. $)
    $( Equinumerosity relation. $)
    breng $p |- ( B e. C -> ( A ~~ B <-> E. f f : A -1-1-onto-> B ) ) $=
      ( vx vy cvv wcel cen wbr cv wf1o wex wb wi wa cop copab wceq f1oeq2
      biexdv f1oeq3 opelopabg df-br df-en eleq2i bitr syl5bb exp wn relen
      elbrexi wfn ccnv f1o4 pm3.26bd cdm visset dmexg ax-mp fndm eleq1d mpbii
      syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZNZOVIVJVOVIVJP
      ABQZEKZFKZVLLZDMZEFRZHZVNVKVTAVRVLLZDMVNEFABGCVQASVSWCDVQAVRVLTUAVRBSWCVM
      DVRBAVLUBUAUCVKVPIHWBABIUDIWAVPEFDUEUFUGUHUIVIUJVOVJVKVIVNABIUKULVMVIDVMV
      LAUMZVIVMWDVLUNBUMABVLUOUPWDVLUQZGHZVIVLGHWFDURVLGUSUTWDWEAGAVLVAVBVCVDVE
      VFVGVH $.
      $([15-Jun-98]$)

    $( Dominance relation. $)
    brdomg $p |- ( B e. C -> ( A ~<_ B <-> E. f f : A -1-1-> B ) ) $=
      ( vx vy cvv wcel cdom wbr cv wf1 wex wb wi wa cop copab wceq f1eq2
      biexdv f1eq3 opelopabg df-br df-dom eleq2i bitr syl5bb exp wn reldom
      elbrexi wf ccnv wfun df-f1 pm3.26bd cdm visset dmexg ax-mp fdm eleq1d
      mpbii syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZNZOVJVKV
      PVJVKPABQZEKZFKZVMLZDMZEFRZHZVOVLWAAVSVMLZDMVOEFABGCVRASVTWDDVRAVSVMTUAVS
      BSWDVNDVSBAVMUBUAUCVLVQIHWCABIUDIWBVQEFDUEUFUGUHUIVJUJVPVKVLVJVOABIUKULVN
      VJDVNABVMUMZVJVNWEVMUNUOABVMUPUQWEVMURZGHZVJVMGHWGDUSVMGUTVAWEWFAGABVMVBV
      CVDVEVFVGVHVI $.
      $([15-Jun-98]$)
  $}

  ${
    $d f x y A $.  $d f x y B $.  $( ` y ` is dummy. $)
    bren.1 $e |- B e. V $.

    $( Equinumerosity relation.  Compare definition of Enderton, p. 129. $)
    bren $p |- ( A ~~ B <-> E. f f : A -1-1-onto-> B ) $=
      ( cvv wcel cen wbr cv wf1o wex wb breng ax-mp ) BEFABGHABCIJCKLDABECMN $.
      $([15-Jun-98]$)

    $( Dominance relation. $)
    brdom $p |- ( A ~<_ B <-> E. f f : A -1-1-> B ) $=
      ( cvv wcel cdom wbr cv wf1 wex wb brdomg ax-mp ) BEFABGHABCIJCKLDABECMN
      $.
      $([15-Jun-98]$)

    $( Dominance in terms of equinumerosity.  Example 1 of Enderton, p. 146. $)
    domen $p |- ( A ~<_ B <-> E. x ( A ~~ x /\ x (_ B ) ) $=
      ( vf cv wf1 wex wf1o wss wa cdom wbr cen visset f11o biex 19.11 bitr
      brdom bren biran 19.41v bitr4 3bitr4 ) BCEFZGZEHZBAFZUFIZUICJZKZEHZAHZBCL
      MBUINMZUKKZAHUHULAHZEHUNUGUQEABCUFEOPQULEARSBCEDTUPUMAUPUJEHZUKKUMUOURUKB
      UIEAOUAUBUJUKEUCUDQUE $.
      $([15-Jun-98]$)
  $}

  $( Strict dominance relation. $)
    brsdom $p |- ( A ~< B <-> ( A ~<_ B /\ -. A ~~ B ) ) $=
      ( cop csdm wcel cdom cen cdif wbr wn wa df-sdom eleq2i df-br bineg bi2an
      eldif bitr4 3bitr4 ) ABCZDETFGHZEZABDIABFIZABGIZJZKZDUATLMABDNUFTFEZTGEZJ
      ZKUBUCUGUEUIABFNUDUHABGNOPTFGQRS $.
      $([25-Jun-98]$)

  ${
    $d x y f $.  $( ` x y f ` are dummy. $)
    $( Equinumerosity implies dominance. $)
    enssdom $p |- ~~ (_ ~<_ $=
      ( vx vy vf cen cdom relen cv cop wf1o wex copab wcel wf1 wfo df-f1o
      pm3.26bd 19.22i opabid 3imtr3 df-en eleq2i df-dom 3imtr4 relssi ) ABDEFAG
      ZBGZHZUEUFCGZIZCJZABKZLZUGUEUFUHMZCJZABKZLZUGDLUGELUJUNULUPUIUMCUIUMUEUFU
      HNUEUFUHOPQUJABRUNABRSDUKUGABCTUAEUOUGABCUBUAUCUD $.
      $([31-Mar-98]$)
  $}

  $( Alternate definition of dominance. $)
  dfdom2 $p |- ~<_ = ( ~< u. ~~ ) $=
    ( cen csdm cun cdom cdif df-sdom uneq2i uncom wss wceq enssdom undif3 mpbi
    3eqtr4r ) ABCADAEZCZBACDBOAFGBAHADIDPJKADLMN $.
    $([17-Jun-98]$)

  $( Equinumerosity implies dominance.  Theorem 15 of Suppes, p. 94. $)
  endom $p |- ( A ~~ B -> A ~<_ B ) $=
    ( cop cen wcel cdom wbr enssdom ssi df-br 3imtr4 ) ABCZDELFEABDGABFGDFLHIAB
    DJABFJK $.
    $([28-May-98]$)

  $( Strict dominance implies dominance. $)
  sdomdom $p |- ( A ~< B -> A ~<_ B ) $=
    ( csdm wbr cdom cen wn brsdom pm3.26bd ) ABCDABEDABFDGABHI $.
    $([10-Jun-98]$)

  $( Strict dominance implies non-equinumerosity. $)
  sdomnen $p |- ( A ~< B -> -. A ~~ B ) $=
    ( csdm wbr cdom cen wn brsdom pm3.27bd ) ABCDABEDABFDGABHI $.
    $([10-Jun-98]$)

  $( Dominance relation.  Theorem 22(iv) of Suppes, p. 97. $)
  brdom2 $p |- ( A ~<_ B <-> ( A ~< B \/ A ~~ B ) ) $=
    ( cop cdom wcel csdm cen cun wbr wo dfdom2 eleq2i df-br bi2or elun bitr4
    3bitr4 ) ABCZDERFGHZEZABDIABFIZABGIZJZDSRKLABDMUCRFEZRGEZJTUAUDUBUEABFMABGM
    NRFGOPQ $.
    $([17-Jun-98]$)

  ${
    $d f A $.  $( ` f ` is dummy. $)
    $( Equinumerosity is reflexive.  Theorem 1 of Suppes, p. 92. $)
    enrefg $p |- ( A e. B -> A ~~ A ) $=
      ( vf wcel cen wbr cv wf1o wex cid cres cvv wfun funi resfunexg mpi f1oi
      f1oeq1 cla4egv syl breng mpbird ) ABDZAAEFAACGZHZCIZUCJAKZLDZUFUCJMUHNJAB
      OPUHAAUGHZUFAQUEUICUGLAAUDUGRSPTAABCUAUB $.
      $([18-Jun-98]$)
  $}

  $( Dominance is reflexive. $)
  domrefg $p |- ( A e. B -> A ~<_ A ) $=
    ( wcel cen wbr cdom enrefg endom syl ) ABCAADEAAFEABGAAHI $.
    $([18-Jun-98]$)

  ${
    $d f A $.  $d f B $.  $d f F $.  $( ` f ` is dummy. $)
    $( Inferring equinumerosity. $)
    f1oeng $p |- ( A e. C -> ( F : A -1-1-onto-> B -> A ~~ B ) ) $=
      ( vf wcel wf1o cv wex cen wbr cvv wi wfn fnex ccnv f1o4 pm3.26bd syl5
      f1oeq1 cla4egv syl6 pm2.43d breng biimprd a1i wn brprc enrefg syl5bir
      a1d com3r pm2.61d syld ) ACFZABDGZABEHZGZEIZABJKZUOUPUSUOUPDLFZUPUSMUODAN
      ZVAUPACDOUPVBDPBNABDQRSURUPEDLABUQDTUAUBUCUOBLFZUSUTMZVCVDMUOVCUTUSABLEUD
      UEUFVCUGZUSUOUTVEUOUTMUSVEUTAAJKUOABJUHACUIUJUKULUMUN $.
      $([19-Jun-98]$)

    $( Inferring dominance. $)
    f1domg $p |- ( A e. C -> ( F : A -1-1-> B -> A ~<_ B ) ) $=
      ( vf wcel wf1 cv wex cdom wbr cvv wi wfn fnex wf f1f ffn syl syl5 f1eq1
      cla4egv syl6 pm2.43d brdomg biimprd a1i wn brprc domrefg syl5bir a1d
      com3r pm2.61d syld ) ACFZABDGZABEHZGZEIZABJKZUPUQUTUPUQDLFZUQUTMUPDANZVBU
      QACDOUQABDPVCABDQABDRSTUSUQEDLABURDUAUBUCUDUPBLFZUTVAMZVDVEMUPVDVAUTABLEU
      EUFUGVDUHZUTUPVAVFUPVAMUTVFVAAAJKUPABJUIACUJUKULUMUNUO $.
      $([18-Jun-98]$)
  $}

  ${
    f1oen.1 $e |- A e. V $.
    $( Inferring equinumerosity. $)
    f1oen $p |- ( F : A -1-1-onto-> B -> A ~~ B ) $=
      ( cvv wcel wf1o cen wbr wi f1oeng ax-mp ) AEFABCGABHIJDABECKL $.
      $([19-Jun-98]$)

    $( Inferring dominance. $)
    f1dom $p |- ( F : A -1-1-> B -> A ~<_ B ) $=
      ( cvv wcel wf1 cdom wbr wi f1domg ax-mp ) AEFABCGABHIJDABECKL $.
      $([19-Jun-98]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Equality implies equinumerosity. $)
    idssen $p |- I (_ ~~ $=
      ( vx vy cid cen reli cv wbr cop wcel weq visset ideq cres wf1o f1oi
      f1oeq3 mpbii f1oen syl sylbi df-br 3imtr3 relssi ) ABCDEAFZBFZCGZUDUEDGZU
      DUEHZCIUHDIUFABJZUGUDUEAKZBKLUIUDUECUDMZNZUGUIUDUDUKNULUDOUDUEUDUKPQUDUEU
      KUJRSTUDUECUAUDUEDUAUBUC $.
      $([30-Apr-98]$)
  $}

  $( The domain of equinumerosity. $)
  dmen $p |- dom ~~ = V $=
    ( cvv cen cdm wss wceq cid idssen dmss dmi sseq1i sylib ax-mp vss ) ABCZDZN
    AEFBDZOGPFCZNDOFBHQANIJKLNML $.
    $([10-May-98]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( A set dominates its subsets.  Theorem 16 of Suppes, p. 94. $)
    ssdomg $p |- ( A e. C -> ( A (_ B -> A ~<_ B ) ) $=
      ( wcel cid cres wf1 cdom wbr wss f1domg wf ccnv wfun wa wfo wf1o f1oi
      f1o3 mpbi pm3.26i fof ax-mp fss mpan funi wceq wb cnvi funeq funres11
      jctir df-f1 sylibr syl5 ) ACDABEAFZGZABHIABJZABCUPKURABUPLZUPMNZOUQURUSUT
      AAUPLZURUSAAUPPZVAVBUTAAUPQVBUTOARAAUPSTUAAAUPUBUCAABUPUDUEEMZNZUTENZVDUF
      EVCUGVEVDUHUIEVCUJUCTAEUKUCULABUPUMUNUO $.
      $([19-Jun-98]$)
  $}

  $( A set dominates its subsets.  Theorem 16 of Suppes, p. 94. $)
  ssdom2g $p |- ( B e. C -> ( A (_ B -> A ~<_ B ) ) $=
    ( wcel wss cdom wbr cvv wi ssexg ssdomg syl6 pm2.43d ) BCDZABEZABFGZNOAHDOP
    IABCJABHKLM $.
    $([19-Jun-98]$)

  ${
    $d f g x y z $.  $( ` f g x y z ` are dummy. $)
    $( Equinumerosity is an equivalence relation. $)
    ener $p |- Er ~~ $=
      ( vx vy vz vf vg cen cv wbr wf1o wex visset bren ccnv f1ocnv f1oen syl
      19.23aiv sylbi wa eeanv ccom f1oco ancoms 19.23aivv sylbir syl2anb ster
      ) ABCFAGZBGZFHZUHUIDGZIZDJUIUHFHZUHUIDBKZLULUMDULUIUHUKMZIUMUHUIUKNUIUHUO
      UNOPQRUHUIEGZIZEJZUICGZUKIZDJZUHUSFHZUJUIUSFHURVASUQUTSZDJEJVBUQUTEDTVCVB
      EDVCUHUSUKUPUAZIZVBUTUQVEUHUIUSUKUPUBUCUHUSVDAKOPUDUEUHUIEUNLUIUSDCKLUFUG
      $.
      $([19-Mar-98]$)
  $}

  $( Strict dominance is irreflexive.  Theorem 21(i) of Suppes, p. 97. $)
  sdomirr $p |- -. A ~< A $=
    ( cvv wcel csdm wbr wn cen sdomnen enrefg nsyl3 relsdom elbrexi con3
    pm2.61i ) ABCZAADEZFPAAGEOAAHABIJPOAADKLMN $.
    $([4-Jun-98]$)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Symmetry of equinumerosity.  Theorem 2 of Suppes, p. 92. $)
    ensymg $p |- ( B e. V -> ( A ~~ B -> B ~~ A ) ) $=
      ( vx vy cvv wcel cen wbr wi relen elbrexi cv wceq breq1 breq2 bi2imd
      visset ener ersym vtocl2i exp syl pm2.43b ) BEFZABGHZBAGHZUEAEFZUDUEUFIZI
      ABGJKUGUDUHCLZDLZGHZUJUIGHZIAUJGHZUJAGHZIUHCDABEEUIAMUKUMULUNUIAUJGNUIAUJ
      GOPUJBMUMUEUNUFUJBAGOUJBAGNPUIUJGCQDQRSTUAUBUC $.
      $([2-Jun-98]$)
  $}

  ${
    $d x y z f g h A $.  $d x y z f g B $.  $d x y z f g h C $.
        $( ` x y z f g h ` are dummy. $)
    $( Transitivity of equinumerosity.  Theorem 3 of Suppes, p. 92. $)
    entr $p |- ( ( A ~~ B /\ B ~~ C ) -> A ~~ C ) $=
      ( vx vy vz cen relen cv visset ener ertr cvv wcel wbr enrefg ax-mp
      vtoclrbr ) DEFABCGHDIZEIFIGDJZEJFJKLSMNSSGOTSMPQR $.
      $([9-Jun-98]$)

    $( Transitivity of dominance relation.  Theorem 17 of Suppes, p. 94. $)
    domtr $p |- ( ( A ~<_ B /\ B ~<_ C ) -> A ~<_ C ) $=
      ( vx vy vz vg vf cdom reldom cv wf1 wex wbr wa eeanv ccom f1co ancoms
      visset f1dom syl 19.23aivv sylbir brdom syl2anb cvv wcel domrefg ax-mp
      vtoclrbr ) DEFABCIJDKZEKZGKZLZGMZUMFKZHKZLZHMZULUQINZULUMINUMUQINUPUTOUOU
      SOZHMGMVAUOUSGHPVBVAGHVBULUQURUNQZLZVAUSUOVDULUMUQURUNRSULUQVCDTZUAUBUCUD
      ULUMGETUEUMUQHFTUEUFULUGUHULULINVEULUGUIUJUK $.
      $([4-Jun-98]$)
  $}

  $( Transitivity of equinumerosity and dominance. $)
  endomtr $p |- ( ( A ~~ B /\ B ~<_ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan ) ABDEBCDEACDEABFEABCGABHI $.
    $([7-Jun-98]$)

  $( Transitivity of dominance and equinumerosity. $)
  domentr $p |- ( ( A ~<_ B /\ B ~~ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan5 ) ABDEBCDEACDEBCFEABCGBCHI $.
    $([7-Jun-98]$)

  ${
    $d f A $.  $( ` f ` is dummy. $)
    $( The empty set is equinumerous only to itself. $)
    en0 $p |- ( A ~~ (/) <-> A = (/) ) $=
      ( vf c0 cen wbr wceq cv wf1o wex zfnull bren ccnv f1ocnv f1o0 pm3.27bd
      syl 19.23aiv sylbi cvv wcel enrefg ax-mp breq1 mpbiri impbi ) ACDEZACFZUF
      ACBGZHZBIUGACBJKUIUGBUICAUHLZHZUGACUHMUKUJCFUGAUJNOPQRUGUFCCDEZCSTULJCSUA
      UBACCDUCUDUE $.
      $([27-May-98]$)
  $}

  ${
    $d f g h A $.  $d f g h B $.  $d f g h C $.  $d f g h D $.
        $( ` f g h ` are dummy. $)
    $( Equinumerosity of union.  Theorem 4 of Suppes, p. 92. $)
    enun $p |- ( ( ( A ~~ B /\ C ~~ D ) /\
  ( ( A i^i C ) = (/) /\ ( B i^i D ) = (/) ) ) -> ( A u. C ) ~~ ( B u. D ) ) $=
      ( vf vg vh cun cvv wcel cen wbr wa cin c0 wceq wi cv wf1o wex wb unexbg
      breng bi2anan9 sylbir f1oun visset unex f1oeq1 cla4ev syl syl5bir exp3a
      19.23advv eeanv syl5ibr sylbid imp3a wn brprc relen elbrexi im2an sylib
      enrefg adantrd pm2.61i ) BDHZIJZABKLZCDKLZMZACNOPBDNOPMZMACHZVHKLZQVIVLVM
      VOVIVLABERZSZETZCDFRZSZFTZMZVMVOQZVIBIJZDIJZMVLWBUABDUBWDVJVRWEVKWAABIEUC
      CDIFUCUDUEVIVQVTMZFTETWCWBVIWFWCEFVIWFVMVOVIVOVNVHGRZSZGTZWFVMMZVNVHIGUCW
      JVNVHVPVSHZSZWIABCDVPVSUFWHWLGWKVPVSEUGFUGUHVNVHWGWKUIUJUKULUMUNVQVTEFUOU
      PUQURVIUSZVLVOVMWMVOVNVNKLZVLVNVHKUTVLVNIJZWNVLAIJZCIJZMWOVJWPVKWQABKVAVB
      CDKVAVBVCACUBVDVNIVEUKULVFVG $.
      $([11-Jun-98]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x D $.  $d x f $.  $d x g $.
    sbthlem.1 $e |- A e. V $.
    sbthlem.2 $e |- D = { x |
                   ( x (_ A /\ ( g " ( B \ ( f " x ) ) ) (_ ( A \ x ) ) } $.
    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem1 $p |- U. D (_ ( A \ ( g " ( B \ ( f " U. D ) ) ) ) $=
      ( cv wcel cuni cima cdif wss wi ssuniss sstr wa cleqabi ssconb biimprd
      exp difss mpi syl5 pm2.43d imp sylbi elssuni imass2 sscon 3syl syl13 mpg
      ) AIZDJZUOBFIZCEIZDKZLZMZLZMZNZOUSVCNAADVCPUOBUQCURUOLZMZLZMZNZVHVCNZVDUP
      UOVHVCQUPUOBNZVGBUOMZNZRZVIVNADHSVKVMVIVKVMVIVKVGBNZVMVIOZVMVKVOVPVKVORVI
      VMUOVGBTUAUBVMVLBNVOBUOUCVGVLBQUDUEUFUGUHUPVAVFNZVBVGNVJUPUOUSNVEUTNVQUOD
      UIUOUSURUJVEUTCUKULVAVFUQUJVBVGBUKULUMUN $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem2 $p |- ( ran g (_ A ->
                     ( A \ ( g " ( B \ ( f " U. D ) ) ) ) (_ U. D ) $=
      ( cv crn wss cuni cima cdif wcel wa cab difss a1i sbthlem1 imass2 ax-mp
      sscon wb wi imassrn sstr ssconb mpan2 syl mpbiri jca cvv ssex wceq sseq1
      difeq2 sseq2 imaeq2 3syl bitrd bi2and abcl sylibr eleq2i elssuni ) FIZJZB
      KZBVGCEIZDLZMZNZMZNZDOZVOVKKVIVOAIZBKZVGCVJVQMZNZMZBVQNZKZPZAQZOZVPVIVOBK
      ZVGCVJVOMZNZMZBVONZKZPZWFVIWGWLWGVIBVNRZSVIWLVOBWJNKZWJVNKZWOWIVMKZWPVLWH
      KZWQVKVOKWRABCDEFGHTVKVOVJUAUBVLWHCUCUBWIVMVGUAUBWJVNBUCUBVIWJBKZWLWOUDZW
      JVHKVIWSUEVGWIUFWJVHBUGUBWSWGWTWNWJVOBUHUIUJUKULWDWMAVOWGVOUMOWNVOBGUNUBV
      QVOUOZVRWGWCWLVQVOBUPXAWCWAWKKZWLXAWBWKUOWCXBUDVQVOBUQWBWKWAURUJXAVTWIUOZ
      WAWJUOXBWLUDXAVSWHUOXCVQVOVJUSVSWHCUQUJVTWIVGUSWAWJWKUPUTVAVBVCVDDWEVOHVE
      VDVODVFUJ $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem3 $p |- ( ran g (_ A ->
                  ( g " ( B \ ( f " U. D ) ) ) = ( A \ U. D ) ) $=
      ( cv crn wss cuni cima cdif wceq wi imassrn sstr ax-mp ssdifb sylib wa
      sbthlem2 sbthlem1 jctil eqss sylibr difeq2 syl eqtr4d ) FIZJZBKZUKCEIDLZM
      NZMZBBUPNZNZBUNNZUMUPBKZUPUROUPULKUMUTPUKUOQUPULBRSUPBTUAUMUNUQOZUSUROUMU
      NUQKZUQUNKZUBVAUMVCVBABCDEFGHUCABCDEFGHUDUEUNUQUFUGUNUQBUHUIUJ $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem4 $p |- ( ( ( dom g = B /\ ran g (_ A ) /\ Fun `' g ) ->
                  ( `' g " ( A \ U. D ) ) = ( B \ ( f " U. D ) ) ) $=
      ( cv cdm wceq crn wss wa ccnv wfun cuni cima cdif cres difss sseq2
      mpbiri ssdmres sylib dfdm4 syl6eq funcnvres sbthlem3 reseq2 syl
      sylan9eqr rneq sylan9eq exp32 imp31 df-ima syl6reqr ) FIZJZCKZUSLBMZNUSOZ
      PZNCEIDQZRZSZVCBVESZTZLZVCVHRVAVBVDVGVJKZVAVBVDVKVAVBVDNZVGUSVGTZOZLZVJVA
      VGVMJZVOVAVGUTMZVGVPKVAVQVGCMCVFUAUTCVGUBUCVGUSUDUEVMUFUGVLVNVIKVOVJKVDVB
      VNVCUSVGRZTZVIVGUSUHVBVRVHKVSVIKABCDEFGHUIVRVHVCUJUKULVNVIUMUKUNUOUPVCVHU
      QUR $.
      $([27-Mar-98]$)

    ${
      $d x H $.
      sbthlem.3 $e |- H = ( ( f |` U. D ) u. ( `' g |` ( A \ U. D ) ) ) $.

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem5 $p |- ( ( dom f = A /\ ran g (_ A ) -> dom H = A ) $=
        ( cv cdm wceq crn wss wa cuni cdif cun cin cima sbthlem1 difss sstri
        sseq2 mpbiri sseqin1 sylib uneq1d imassrn sbthlem3 sseq1d mpbii uneq2d
        sylan9eq cres ccnv dmeq ax-mp dmun dmres uneq1i df-rn cleqcomi ineq2i
        eqtr uneq2i 3eqtr syl6reqr undif3 mpbi syl6eqr ) EKZLZBMZFKZNZBOZPZGLZD
        QZBWARZSZBVSWCWAVNTZWBVQTZSZVTVOVRWCWDWBSWFVOWAWDWBVOWAVNOZWAWDMVOWGWAB
        OZWABVPCVMWAUARZUAZRBABCDEFHIUBBWJUCUDZVNBWAUEUFWAVNUGUHUIVRWBWEWDVRWBV
        QOZWBWEMVRWJVQOWLVPWIUJVRWJWBVQABCDEFHIUKULUMWBVQUGUHUNUOVTVMWAUPZVPUQZ
        WBUPZSZLZWMLZWOLZSZWFGWPMVTWQMJGWPURUSWMWOUTWTWDWSSWFWRWDWSVMWAVAVBWSWE
        WDWSWBWNLZTWEWNWBVAXAVQWBVQXAVPVCVDVEVFVGVFVHVIWHBWCMWKWABVJVKVL $.
        $([22-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem6 $p |- ( ( ran f (_ B /\
               ( ( dom g = B /\ ran g (_ A ) /\ Fun `' g ) ) -> ran H = B ) $=
        ( cv crn wss cdm wceq wa ccnv wfun cuni cima cdif cun wi imassrn sstr
        ax-mp undif3 sylib cres sbthlem4 df-ima syl5reqr uneq2d rnun rneq
        uneq1i 3eqtr4 syl6eqr sylan9eq cleqcomd ) EKZLZCMZFKZNCOVDLBMPVDQZRPZPC
        GLZVCVFCVADSZTZCVIUAZUBZVGVCVICMZCVKOVIVBMVCVLUCVAVHUDVIVBCUEUFVICUGUHV
        FVKVIVEBVHUAZUIZLZUBZVGVFVJVOVIVFVEVMTVJVOABCDEFHIUJVEVMUKULUMVAVHUIZVN
        UBZLZVQLZVOUBVGVPVQVNUNGVROVGVSOJGVRUOUFVIVTVOVAVHUKUPUQURUSUT $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem7 $p |- ( ( Fun f /\ Fun `' g ) -> Fun H ) $=
        ( cv wfun ccnv wa cuni cres cdif cun cdm cin c0 wceq wss dmres inss1
        eqsstr ssrin ax-mp sslin sstri indif0 sseqtr ss0 funun mpan2 funres
        syl2an wb funeq sylibr ) EKZLZFKMZLZNVADOZPZVCBVEQZPZRZLZGLZVFLZVHLZVJV
        BVDVLVMNVFSZVHSZTZUAUBZVJVPUAUCVQVPVEVGTZUAVPVEVOTZVRVNVEUCVPVSUCVNVEVA
        SZTVEVAVEUDVEVTUEUFVNVEVOUGUHVOVGUCVSVRUCVOVGVCSZTVGVCVGUDVGWAUEUFVOVGV
        EUIUHUJVEBUKULVPUMUHVFVHUNUOVEVAUPVGVCUPUQGVIUBVKVJURJGVIUSUHUT $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem8 $p |- ( ( Fun `' f /\ ( ( ( Fun g /\ dom g = B ) /\ ran g (_ A )
                     /\ Fun `' g ) ) -> Fun `' H ) $=
        ( cv ccnv wfun cdm wceq wa crn wss cuni cres cdif cun cin c0 funun
        funres11 wrel funrel dfrel2 sylib funeq biimpd mpcom syl adantr
        ad2antll im2an cima sbthlem4 df-ima df-rn eqtr syl5eqr eqtr2 jctil
        ineq12 indif0 syl6eq pm3.27 imran sylan adantl sylan13 wb cnveq ax-mp
        cnvun sylibr ) EKZLMZFKZMZWANCOZPZWAQBRZPZWALZMZPZPZVSDSZTZLZWGBWKUAZTZ
        LZUBZMZGLZMZWMMZWPMZPWMNZWPNZUCZUDOZWRWJWMWPUEVTXAWIXBWKVSUFWDXBWEWHWBX
        BWCWBWGLZMZXBWAXGOZWBXHWBWAUGXIWAUHWAUIUJXIWBXHWAXGUKULUMWNWGUFUNUOUPUQ
        WIXFVTWCWEPZWHXFWFXJWHPZXEVSWKURZCXLUAZUCZUDXKXCXLOZXDXMOZPXEXNOXKXPXOX
        KWGWNURZXMXDABCDEFHIUSXQWOQXDWGWNUTWOVAVBVCXLWLQXCVSWKUTWLVAVDVEXCXLXDX
        MVFUNXLCVGVHWDWCWEWBWCVIVJVKVLVMWSWQOWTWRVNWSWLWOUBZLZWQGXROWSXSOJGXRVO
        VPWLWOVQVBWSWQUKVPVR $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem9 $p |- ( ( f : A -1-1-> B /\ g : B -1-1-> A ) ->
                    H : A -1-1-onto-> B ) $=
        ( cv wfun cdm wceq wa crn wss ccnv wf1 wf1o sbthlem7 sbthlem5 adantarl
        im2an an41r3s adantalr sbthlem8 adantall sbthlem6 df-rn syl5eqr pm3.27
        imran sylan5 jca wf df-f1 wfn df-f df-fn biran bitr bi2an f1o4 3imtr4
        ) EKZLZVFMBNZOZVFPCQZOZVFRLZOZFKZLZVNMCNZOZVNPBQZOZVNRLZOZOZGLZGMBNZOZG
        RZLZWFMZCNZOZOZBCVFSZCBVNSZOBCGTZWBWEWJVKWAWEVLVIWAWEVJVGVTVHVSWEVGVTOW
        CVHVSOWDABCDEFGHIJUAVHVRWDVQABCDEFGHIJUBUCUDUEUFUFWBWGWIVLWAWGVKABCDEFG
        HIJUGUHVKWAWIVLVJWAWIVIVJVPVROZVTOZWIWAVJWPOGPCWHABCDEFGHIJUIGUJUKVSWOV
        TVQVPVRVOVPULUMUMUNUHUFUOUOWLVMWMWAWLBCVFUPZVLOVMBCVFUQWQVKVLWQVFBURZVJ
        OVKBCVFUSWRVIVJVFBUTVAVBVAVBWMCBVNUPZVTOWACBVNUQWSVSVTWSVNCURZVROVSCBVN
        USWTVQVRVNCUTVAVBVAVBVCWNGBURZWFCURZOWKBCGVDXAWEXBWJGBUTWFCUTVCVBVE $.
        $([28-Mar-98]$)

      ${
        $d f g A $.  $d f g B $.
        sbthlem.4 $e |- B e. V $.
        $( Lemma for Schroder-Bernstein Theorem. $)
        sbthlem10 $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
          ( cdom wbr wa cv wf1 wex cen brdom bi2an eeanv bitr4 wf1o sbthlem9
          f1oen syl 19.23aivv sylbi ) BCLMZCBLMZNZBCEOPZCBFOPZNZFQEQZBCRMZUKULE
          QZUMFQZNUOUIUQUJURBCEKSCBFHSTULUMEFUAUBUNUPEFUNBCGUCUPABCDEFGHIJUDBCG
          HUEUFUGUH $.
          $([28-Mar-98]$)
      $}
    $}

  $}

  ${
    $d x y z w f g A $.  $d x y z w f g B $.  $( ` x y z w f g ` are dummy. $)
    $( Schroder-Bernstein Theorem.  Theorem 18 of Suppes, p. 95.  The main
       proof is in ~ sbthlem10 ; this proof mainly changes bound variables
       to eliminate the hypotheses of ~ sbthlem10 . $)
    sbth $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
      ( vz vw vx vy vg vf cdom wbr wa cen cvv wcel wi cv wceq breq1 breq2
      bi2and bi2imd wss cima cdif cab cuni cres ccnv cun visset weq sseq1 wb
      imaeq2 difeq2 syl 3syl sseq2 bitrd cbvabv cleqid sbthlem10 vtocl2i
      reldom elbrexi syl2an pm2.43i ) ABIJZBAIJZKZABLJZAMNBMNVJVKOZVHVICPZDPZIJ
      ZVNVMIJZKZVMVNLJZOAVNIJZVNAIJZKZAVNLJZOVLCDABMMVMAQZVQWAVRWBWCVOVSVPVTVMA
      VNIRVMAVNISTVMAVNLRUAVNBQZWAVJWBVKWDVSVHVTVIVNBAISVNBAIRTVNBALSUAEVMVNFPZ
      VMUBZGPZVNHPZWEUCZUDZUCZVMWEUDZUBZKZFUEZHGWHWOUFZUGWGUHVMWPUDUGUIZCUJWNEP
      ZVMUBZWGVNWHWRUCZUDZUCZVMWRUDZUBZKFEFEUKZWFWSWMXDWEWRVMULXEWMXBWLUBZXDXEW
      JXAQZWKXBQWMXFUMXEWIWTQXGWEWRWHUNWIWTVNUOUPWJXAWGUNWKXBWLULUQXEWLXCQXFXDU
      MWEWRVMUOWLXCXBURUPUSTUTWQVADUJVBVCABIVDVEBAIVDVEVFVG $.
      $([8-Jun-98]$)
  $}

  $( Schroder-Bernstein Theorem and its converse. $)
  sbthbg $p |- ( B e. V -> ( A ~~ B <-> ( A ~<_ B /\ B ~<_ A ) ) ) $=
    ( cvv wcel cen wbr cdom wa wi endom a1i ensymg syl6 jcad sbth impbid ) BCDZ
    ABEFZABGFZBAGFZHZQRSTRSIQABJKQRBAEFTABLBAJMNUARIQABOKP $.
    $([8-Jun-98]$)

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Schroder-Bernstein Theorem in class form. $)
    sbthcl $p |- ~~ = ( ~<_ i^i `' ~<_ ) $=
      ( vx vy cen cdom ccnv cin relen wrel reldom relin ax-mp cv wbr wa cop
      wcel cvv wb visset sbthbg df-br opelcnv bitr4 bi2an elin 3bitr3 cleqreli
      ) ABCDDEZFZGDHUIHIDUHJKALZBLZCMZUJUKDMZUKUJDMZNZUJUKOZCPUPUIPZUKQPULUORBS
      ZUJUKTKUJUKCUAUOUPDPZUPUHPZNUQUMUSUNUTUJUKDUAUNUKUJODPUTUKUJDUAUJUKDASURU
      BUCUDUPDUHUEUCUFUG $.
      $([28-Mar-98]$)
  $}

  $( Alternate definition of strict dominance.  Compare Definition 3 of
     Suppes, p. 97. $)
  dfsdom2 $p |- ~< = ( ~<_ \ `' ~<_ ) $=
    ( cdom cen cdif ccnv cin csdm wceq sbthcl difeq2 ax-mp df-sdom difin
    3eqtr4 ) ABCZAAADZEZCZFAOCBPGNQGHBPAIJKAOLM $.
    $([31-Mar-98]$)

  $( Strict dominance is not symmetric.  Theorem 21(ii) of Suppes, p. 97. $)
  sdomnsym $p |- ( A ~< B -> -. B ~< A ) $=
    ( csdm wbr cen sdomnen cdom sbth sdomdom syl2an exp mtod ) ABCDZBACDZABEDZA
    BFMNOABGDBAGDOMNABHABIBAIJKL $.
    $([8-Jun-98]$)

  $( Theorem 22(i) of Suppes, p. 97. $)
  domnsym $p |- ( A ~<_ B -> -. B ~< A ) $=
    ( cdom wbr csdm cen wo wn brdom2 sdomnsym cvv wcel ensymg pm2.16d relsdom
    elbrexi sdomnen syl13 con2 jaoi sylbi ) ABCDABEDZABFDZGBAEDZHZABIUBUEUCABJU
    DUCBKLZBAFDZHUCHUDUFUCUGABMNBAEOPBAQRSTUA $.
    $([10-Jun-98]$)

  $( Transitivity of strict dominance and dominance.  Theorem 22(iii) of
     Suppes, p. 97. $)
  sdomdomtr $p |- ( C e. V -> ( ( A ~< B /\ B ~<_ C ) -> A ~< C ) ) $=
    ( cvv wcel csdm wbr cdom wa cen wn sdomnen ad2antrl wi domtr sdomdom sylan
    wo brdom2 df-or bitr sylib adantl ensymg endom syl6 sylan9r a1d jcad
    pm3.27 sbth entr mt3d exp ) CDEZABFGZBCHGZIZACFGZUOURIZUSABJGZUPVAKUOUQABLM
    UTUSKZACJGZCBJGZIVAUTVBVCVDURVBVCNZUOURACHGZVEABHGZUQVFUPABCOABPZQVFUSVCRVE
    ACSUSVCTUAUBZUCUTVBCBHGZUQIVDUTVBVJUQUTVBCAHGZVGIVJUTVBVKVGURVBVCUOVKVIUOVC
    CAJGVKACUDCAUEUFUGUTVGVBUPVGUOUQVHMUHUICABOUFUTUQVBURUQUOUPUQUJUCUHUICBUKUF
    UIACBULUFUMUN $.
    $([9-Jun-98]$)

  $( Strict dominance is transitive.  Theorem 21(iii) of Suppes, p. 97. $)
  sdomtr $p |- ( ( A ~< B /\ B ~< C ) -> A ~< C ) $=
    ( csdm wbr cvv wcel wa wi wn sdomirr brprc mtbiri a3i cdom sdomdomtr
    sdomdom sylan5d syl anabsi7 ) ABDEZBCDEZACDEZUBCFGZUAUBHUCIUDUBUDJUBBBDEBKB
    CDLMNUDUABCOEUCUBABCPBCQRST $.
    $([9-Jun-98]$)

  $( Transitivity of dominance and strict dominance.  Theorem 22(ii) of
     Suppes, p. 97. $)
  domsdomtr $p |- ( ( A ~<_ B /\ B ~< C ) -> A ~< C ) $=
    ( cdom wbr csdm cen wo wi brdom2 sdomtr exp cvv wcel relsdom elbrexi wa wn
    endomtr adantl ensymg entr syl6 imp pm2.16d im2and brsdom 3imtr4g syl
    pm2.43b jaoi sylbi ) ABDEZBCFEZACFEZUMABFEZABGEZHUNUOIZABJUPURUQUPUNUOABCKL
    UQUNUOUNBMNZUQURIBCFOPUSUQURUSUQQZBCDEZBCGEZRZQACDEZACGEZRZQUNUOUTVAVDVCVFU
    QVAVDIUSUQVAVDABCSLTUTVEVBUSUQVEVBIZUSUQBAGEZVGABUAVHVEVBBACUBLUCUDUEUFBCUG
    ACUGUHLUIUJUKULUD $.
    $([10-Jun-98]$)

  ${
    $d x y z w f A $.  $( ` x y z w f ` are dummy. $)
    canth2.1 $e |- A e. V $.
    $( Cantor's Theorem.  Theorem 23 of Suppes, p. 97. $)
    canth2 $p |- A ~< P~ A $=
      ( vz vw vx vy vf cpw csdm wbr cdom cen wn wa cv wcel csn wceq copab wf1
      wf cfv weq wi wral visset snelpw biimp rgen cleqid fopab2 ax-mp sneq
      snex opabval2 cleqan12d sneqr syl6bi rgen2 pm3.2i f1fv mpbir f1dom wfo
      wex canth nex wf1o zfpowcl bren df-f1o pm3.27bd 19.22i sylbi con3 brsdom
      ) AAHZIJAVQKJZAVQLJZMZNVRVTAVQCOZAPZDOWAQZRNCDSZTZVRWEAVQWDUAZEOZWDUBZFOZ
      WDUBZRZEFUCZUDZFAUEEAUEZNWFWNWCVQPZCAUEWFWOCAWBWOWAACUFUGUHUICDAVQWCWDWDU
      JZUKULWMEFAWGAPZWIAPZNWKWGQZWIQZRWLWQWRWHWSWJWTCDWGWCWSAWDWAWGUMWPWGUNUOC
      DWIWCWTAWDWAWIUMWPWIUNUOUPWGWIEUFUQURUSUTEFAVQWDVAVBAVQWDBVCULAVQGOZVDZGV
      EZMVTXBGAXABVFVGVSXCVSAVQXAVHZGVEXCAVQGABVIVJXDXBGXDAVQXATXBAVQXAVKVLVMVN
      VOULUTAVQVPVB $.
      $([1-Apr-98]$)
  $}

