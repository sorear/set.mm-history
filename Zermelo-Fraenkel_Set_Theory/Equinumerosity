$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equinumerosity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Introduce new constant symbols. $)
  $c ~~ $. $( Equinumerosity relation ("approximately equals" symbol) $)
  $c ~<_ $. $( Dominance relation (curly less-than-or-equal) $)
  $c ~< $. $( Strict dominance relation (curly less-than) $)

  $( Extend class definition to include equinumerosity relation
     ("approximately equals" symbol) $)
  cen $a class ~~ $.
  $( Extend class definition to include dominance relation (curly
     less-than-or-equal) $)
  cdom $a class ~<_ $.
  $( Extend class definition to include strict dominance relation (curly
     less-than) $)
  csdm $a class ~< $.

  ${
    $d x y f $.  $d x y f $.
    $( Define equinumerosity relation.  Definition of [Enderton]
       p. 129, except we consider ` ~~ ` to be a (proper) class rather
       than a connective.  This is acceptable because equinumerosity is
       meaningful for sets only and not for proper classes.  We derive
       the usual definition as ~ bren . $)
    df-en $a |- ~~ = { <. x , y >. | E. f f : x -1-1-onto-> y } $.

    $( Define dominance relation.  For an alternate definition see
       ~ dfdom2 . Compare Definition of [Enderton] p. 145.  Typical
       textbook definitions are derived as ~ brdom and ~ domen . $)
    df-dom $a |- ~<_ = { <. x , y >. | E. f f : x -1-1-> y } $.

    $( Define strict dominance relation.  Alternate possible definitions are
       derived as ~ brsdom and ~ dfsdom2 .  Definition 3 of [Suppes] p. 97. $)
    df-sdom $a |- ~< = ( ~<_ \ ~~ ) $.

  $}

  ${
    $d x y f $.  $d x y f $.
    $( Equinumerosity is a relation. $)
    relen $p |- Rel ~~ $=
      ( vx vy vf cen wrel cv wf1o wex copab relopab wceq wb df-en releq ax-mp
      mpbir ) DEZAFBFCFGCHZABIZEZRABJDSKQTLABCMDSNOP $.
      $([28-Mar-98]$)

    $( Dominance is a relation. $)
    reldom $p |- Rel ~<_ $=
      ( vx vy vf cdom wrel cv wf1 wex copab relopab wceq wb df-dom releq ax-mp
      mpbir ) DEZAFBFCFGCHZABIZEZRABJDSKQTLABCMDSNOP $.
      $([28-Mar-98]$)
  $}

  $( Strict dominance is a relation. $)
  relsdom $p |- Rel ~< $=
    ( cdom wrel csdm reldom cen cdif reldif wceq wb df-sdom releq ax-mp sylibr
    ) ABZCBZDNAEFZBZOAEGCPHOQIJCPKLML $.
    $([31-Mar-98]$)

  ${
    $d f x y A $.  $d f x y B $.  $d y C $.  $( ` x y ` are dummy. $)
    $( Equinumerosity relation. $)
    breng $p |- ( B e. C -> ( A ~~ B <-> E. f f : A -1-1-onto-> B ) ) $=
      ( vx vy cvv wcel cen wbr cv wf1o wex wb wi wceq f1oeq2 biexdv f1oeq3
      df-en brabg exp wn relen elbrexi wfn f1ofn cdm visset dmexg ax-mp fndm
      eleq1d mpbii syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZN
      ZOUTVAVFEKZFKZVCLZDMAVHVCLZDMVEEFABGCIVGAPVIVJDVGAVHVCQRVHBPVJVDDVHBAVCSR
      EFDTUAUBUTUCVFVAVBUTVEABIUDUEVDUTDVDVCAUFZUTABVCUGVKVCUHZGHZUTVCGHVMDUIVC
      GUJUKVKVLAGAVCULUMUNUOUPUQURUS $.
      $([15-Jun-98]$)

    $( Dominance relation. $)
    brdomg $p |- ( B e. C -> ( A ~<_ B <-> E. f f : A -1-1-> B ) ) $=
      ( vx vy cvv wcel cdom wbr cv wf1 wex wb wi wceq f1eq2 biexdv f1eq3
      df-dom brabg exp wn reldom elbrexi wf f1f cdm visset dmexg ax-mp fdm
      eleq1d mpbii syl 19.23aiv pm5.21ni a1d pm2.61i ) AGHZBCHZABIJZABDKZLZDMZN
      ZOUTVAVFEKZFKZVCLZDMAVHVCLZDMVEEFABGCIVGAPVIVJDVGAVHVCQRVHBPVJVDDVHBAVCSR
      EFDTUAUBUTUCVFVAVBUTVEABIUDUEVDUTDVDABVCUFZUTABVCUGVKVCUHZGHZUTVCGHVMDUIV
      CGUJUKVKVLAGABVCULUMUNUOUPUQURUS $.
      $([15-Jun-98]$)
  $}

  ${
    $d f x y A $.  $d f x y B $.  $( ` y ` is dummy. $)
    bren.1 $e |- B e. V $.

    $( Equinumerosity relation.  Compare Definition of [Enderton] p. 129. $)
    bren $p |- ( A ~~ B <-> E. f f : A -1-1-onto-> B ) $=
      ( cvv wcel cen wbr cv wf1o wex wb breng ax-mp ) BEFABGHABCIJCKLDABECMN $.
      $([15-Jun-98]$)

    $( Dominance relation. $)
    brdom $p |- ( A ~<_ B <-> E. f f : A -1-1-> B ) $=
      ( cvv wcel cdom wbr cv wf1 wex wb brdomg ax-mp ) BEFABGHABCIJCKLDABECMN
      $.
      $([15-Jun-98]$)

    $( Dominance in terms of equinumerosity.  Example 1 of [Enderton] p. 146. $)
    domen $p |- ( A ~<_ B <-> E. x ( A ~~ x /\ x (_ B ) ) $=
      ( vf cv wf1 wex wf1o wss wa cdom wbr cen visset f11o biex 19.11 bitr
      brdom bren biran 19.41v bitr4 3bitr4 ) BCEFZGZEHZBAFZUFIZUICJZKZEHZAHZBCL
      MBUINMZUKKZAHUHULAHZEHUNUGUQEABCUFEOPQULEARSBCEDTUPUMAUPUJEHZUKKUMUOURUKB
      UIEAOUAUBUJUKEUCUDQUE $.
      $([15-Jun-98]$)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Dominance in terms of equinumerosity, with the sethood requirement
       expressed as an antecedent.  Example 1 of [Enderton] p. 146. $)
    domeng $p |- ( B e. C -> ( A ~<_ B <-> E. x ( A ~~ x /\ x (_ B ) ) ) $=
      ( vy cv cdom wbr cen wss wa wex wb wceq breq2 sseq2 biland biexdv bi2bid
      visset domen vtoclg ) BEFZGHZBAFZIHZUEUCJZKZALZMBCGHZUFUECJZKZALZMECDUCCN
      ZUDUJUIUMUCCBGOUNUHULAUNUGUKUFUCCUEPQRSABUCETUAUB $.
      $([?]$)
  $}

    $( Strict dominance relation, meaning " ` B ` is strictly greater in size
       than ` A ` ."  Definition of [Mendelson] p. 255. $)
    brsdom $p |- ( A ~< B <-> ( A ~<_ B /\ -. A ~~ B ) ) $=
      ( cop csdm wcel cdom cen cdif wbr wn wa df-sdom eleq2i df-br bineg bi2an
      eldif bitr4 3bitr4 ) ABCZDETFGHZEZABDIABFIZABGIZJZKZDUATLMABDNUFTFEZTGEZJ
      ZKUBUCUGUEUIABFNUDUHABGNOPTFGQRS $.
      $([25-Jun-98]$)

  ${
    $d x y f $.  $( ` x y f ` are dummy. $)
    $( Equinumerosity implies dominance. $)
    enssdom $p |- ~~ (_ ~<_ $=
      ( vx vy vf cen cdom relen cv cop wf1o wex copab wcel wf1 f1of1 19.22i
      opabid 3imtr4 df-en eleq2i df-dom relssi ) ABDEFAGZBGZHZUBUCCGZIZCJZABKZL
      ZUDUBUCUEMZCJZABKZLZUDDLUDELUGUKUIUMUFUJCUBUCUENOUGABPUKABPQDUHUDABCRSEUL
      UDABCTSQUA $.
      $([31-Mar-98]$)
  $}

  $( Alternate definition of dominance. $)
  dfdom2 $p |- ~<_ = ( ~< u. ~~ ) $=
    ( cen csdm cun cdom cdif df-sdom uneq2i uncom wss wceq enssdom undif3 mpbi
    3eqtr4r ) ABCADAEZCZBACDBOAFGBAHADIDPJKADLMN $.
    $([17-Jun-98]$)

  $( Equinumerosity implies dominance.  Theorem 15 of [Suppes] p. 94. $)
  endom $p |- ( A ~~ B -> A ~<_ B ) $=
    ( cop cen wcel cdom wbr enssdom ssi df-br 3imtr4 ) ABCZDELFEABDGABFGDFLHIAB
    DJABFJK $.
    $([28-May-98]$)

  $( Strict dominance implies dominance. $)
  sdomdom $p |- ( A ~< B -> A ~<_ B ) $=
    ( csdm wbr cdom cen wn brsdom pm3.26bd ) ABCDABEDABFDGABHI $.
    $([10-Jun-98]$)

  $( Strict dominance implies non-equinumerosity. $)
  sdomnen $p |- ( A ~< B -> -. A ~~ B ) $=
    ( csdm wbr cdom cen wn brsdom pm3.27bd ) ABCDABEDABFDGABHI $.
    $([10-Jun-98]$)

  $( Dominance in terms of strict dominance and equinumerosity.  Theorem 22(iv)
     of [Suppes] p. 97. $)
  brdom2 $p |- ( A ~<_ B <-> ( A ~< B \/ A ~~ B ) ) $=
    ( cop cdom wcel csdm cen cun wbr wo dfdom2 eleq2i df-br bi2or elun bitr4
    3bitr4 ) ABCZDERFGHZEZABDIABFIZABGIZJZDSRKLABDMUCRFEZRGEZJTUAUDUBUEABFMABGM
    NRFGOPQ $.
    $([17-Jun-98]$)

  ${
    $d f A $.  $( ` f ` is dummy. $)
    $( Equinumerosity is reflexive.  Theorem 1 of [Suppes] p. 92. $)
    enrefg $p |- ( A e. B -> A ~~ A ) $=
      ( vf wcel cen wbr cv wf1o wex cid cres cvv wfun funi resfunexg mpi f1oi
      f1oeq1 cla4egv syl breng mpbird ) ABDZAAEFAACGZHZCIZUCJAKZLDZUFUCJMUHNJAB
      OPUHAAUGHZUFAQUEUICUGLAAUDUGRSPTAABCUAUB $.
      $([18-Jun-98]$)
  $}

  $( Equality implies equinumerosity. $)
  eqeng $p |- ( A e. C -> ( A = B -> A ~~ B ) ) $=
    ( wceq wcel cen wbr breq2 enrefg syl5bi comm ) ABDZACEZABFGZLAAFGNMABAFHACI
    JK $.
    $([8-Nov-03]$)

  $( Dominance is reflexive. $)
  domrefg $p |- ( A e. B -> A ~<_ A ) $=
    ( wcel cen wbr cdom enrefg endom syl ) ABCAADEAAFEABGAAHI $.
    $([18-Jun-98]$)

  ${
    $d f A $.  $d f B $.  $d f F $.  $( ` f ` is dummy. $)
    $( The domain and range of a one-to-one, onto function are equinumerous. $)
    f1oeng $p |- ( A e. C -> ( F : A -1-1-onto-> B -> A ~~ B ) ) $=
      ( vf wcel wf1o cv wex cen wbr cvv wi wfn fnex f1ofn syl5 f1oeq1 cla4egv
      syl6 pm2.43d wn brprc enrefg syl5bir a1d com3r breng biimprd pm2.61d2
      syld ) ACFZABDGZABEHZGZEIZABJKZULUMUPULUMDLFZUMUPMULDANURUMACDOABDPQUOUME
      DLABUNDRSTUAULBLFZUPUQMUSUBZUPULUQUTULUQMUPUTUQAAJKULABJUCACUDUEUFUGUSUQU
      PABLEUHUIUJUK $.
      $([19-Jun-98]$)

    $( The domain of a one-to-one function is dominated by its range. $)
    f1domg $p |- ( A e. C -> ( F : A -1-1-> B -> A ~<_ B ) ) $=
      ( vf wcel wf1 cv wex cdom wbr cvv wi wfn fnex wf f1f ffn syl syl5 f1eq1
      cla4egv syl6 pm2.43d wn brprc domrefg syl5bir a1d com3r brdomg biimprd
      pm2.61d2 syld ) ACFZABDGZABEHZGZEIZABJKZUOUPUSUOUPDLFZUPUSMUODANZVAUPACDO
      UPABDPVBABDQABDRSTURUPEDLABUQDUAUBUCUDUOBLFZUSUTMVCUEZUSUOUTVDUOUTMUSVDUT
      AAJKUOABJUFACUGUHUIUJVCUTUSABLEUKULUMUN $.
      $([18-Jun-98]$)
  $}

  ${
    f1oen.1 $e |- A e. V $.
    $( The domain and range of a one-to-one, onto function are equinumerous. $)
    f1oen $p |- ( F : A -1-1-onto-> B -> A ~~ B ) $=
      ( cvv wcel wf1o cen wbr wi f1oeng ax-mp ) AEFABCGABHIJDABECKL $.
      $([19-Jun-98]$)

    $( The domain of a one-to-one function is dominated by its range. $)
    f1dom $p |- ( F : A -1-1-> B -> A ~<_ B ) $=
      ( cvv wcel wf1 cdom wbr wi f1domg ax-mp ) AEFABCGABHIJDABECKL $.
      $([19-Jun-98]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    en2.1 $e |- A e. V $.
    en2.2 $e |- ( x e. A -> C e. V ) $.
    en2.3 $e |- ( y e. B -> D e. V ) $.
    en2.4 $e |- ( ( x e. A /\ y = C ) <-> ( y e. B /\ x = D ) ) $.
    $( Equinumerosity inference from an implicit one-to-one onto function. $)
    en2 $p |- A ~~ B $=
      ( cv wcel wceq wa copab wf1o cen wbr wfn ccnv cvv weu eueq sylib bicom
      biopab fnopab cnvopab pm3.2i f1o4 mpbir f1oen ax-mp ) CDBKZDLZAKZFMZNZABO
      ZPZCDQRUTUSCSZUSTZDSZNVAVCUNEMZABCUSUPCLZEUALVDBUBHBEUCUDURVEVDNZABVFURJU
      EUFUGUQBADVBUOFUALUQAUBIAFUCUDURABUHUGUICDUSUJUKCDUSGULUM $.
      $([?]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y z C $.  $d x z D $.
    dom2.1 $e |- ( x e. A -> C e. B ) $.
    dom2.2 $e |- ( ( x e. A /\ y e. A ) -> ( C = D <-> x = y ) ) $.
    $( A mapping (first hypothesis) that is one-to-one (second hypothesis)
       implies its domain is dominated by its range. $)
    dom2 $p |- ( A e. R -> A ~<_ B ) $=
      ( vz wcel cv wceq wa copab wf1 cdom wbr wf cfv weq wi wral rgen cleqid
      fopab2 mpbi fvopab2 mpian ax-17 hbopab1 wel hbfv hbeq hbim eleq1
      birimd wb birand anidm syl6bb fveq2 adantr biimparc cleq12d exp sylbird
      pm5.74d bitrd chv2 cleqan12d biimpd sylbid rgen2 pm3.2i hbopab2 f1fvf
      mpbir f1domg mpi ) CGKCDALZCKZBLZEMNZABOZPZCDQRWFCDWESZWAWETZWCWETZMZABUA
      ZUBZBCUCACUCZNWGWMEDKZACUCWGWNACHUDABCDEWEWEUEUFUGWLABCWBWCCKZNZWJEFMZWKW
      BWOWHEWIFWBWNWHEMZHABCEDUHUIZWBWRUBZWOWIFMZUBZABWOXAAWOAUJAJJWIFAJWCWEWDA
      BJUKZJBULAUJUMJLFKAUJUNUOWKWTWOWRUBXBWKWBWOWRWAWCCUPZUQWKWOWRXAWKWOWPWRXA
      URZWKWPWOWONWOWKWBWOWOXDUSWOUTVAWKWPXEWKWPNWHWIEFWKWJWPWAWCWEVBVCWPWQWKIV
      DVEVFVGVHVIWSVJVKWPWQWKIVLVMVNVOABJCDWEXCWDABJVPVQVRCDGWEVSVT $.
      $([30-Oct-03]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Equality implies equinumerosity. $)
    idssen $p |- I (_ ~~ $=
      ( vx vy cid cen reli cv wbr cop wcel weq visset ideq cres wf1o f1oi
      f1oeq3 mpbii f1oen syl sylbi df-br 3imtr3 relssi ) ABCDEAFZBFZCGZUDUEDGZU
      DUEHZCIUHDIUFABJZUGUDUEAKZBKLUIUDUECUDMZNZUGUIUDUDUKNULUDOUDUEUDUKPQUDUEU
      KUJRSTUDUECUAUDUEDUAUBUC $.
      $([30-Apr-98]$)
  $}

  $( The domain of equinumerosity. $)
  dmen $p |- dom ~~ = V $=
    ( cvv cen cdm wss wceq cid idssen dmss dmi sseq1i sylib ax-mp vss mpbi ) AB
    CZDZOAEFBDZPGQFCZODPFBHRAOIJKLOMN $.
    $([10-May-98]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( A set dominates its subsets.  Theorem 16 of [Suppes] p. 94. $)
    ssdomg $p |- ( A e. C -> ( A (_ B -> A ~<_ B ) ) $=
      ( wcel cid cres wf1 cdom wbr wss f1domg wf ccnv wfun wa wfo wf1o f1oi
      f1o3 mpbi pm3.26i fof ax-mp fss mpan funi wceq wb cnvi funeq funres11
      jctir df-f1 sylibr syl5 ) ACDABEAFZGZABHIABJZABCUPKURABUPLZUPMNZOUQURUSUT
      AAUPLZURUSAAUPPZVAVBUTAAUPQVBUTOARAAUPSTUAAAUPUBUCAABUPUDUEEMZNZUTENZVDUF
      EVCUGVEVDUHUIEVCUJUCTAEUKUCULABUPUMUNUO $.
      $([19-Jun-98]$)
  $}

  $( A set dominates its subsets.  Theorem 16 of [Suppes] p. 94. $)
  ssdom2g $p |- ( B e. C -> ( A (_ B -> A ~<_ B ) ) $=
    ( wcel wss cdom wbr cvv wi ssexg ssdomg syl6 pm2.43d ) BCDZABEZABFGZNOAHDOP
    IABCJABHKLM $.
    $([19-Jun-98]$)

  ${
    $d f g x y z $.  $( ` f g x y z ` are dummy. $)
    $( Equinumerosity is an equivalence relation. $)
    ener $p |- Er ~~ $=
      ( vx vy vz vf vg cen cv wbr wf1o wex visset bren ccnv f1ocnv f1oen syl
      19.23aiv sylbi wa eeanv ccom f1oco ancoms 19.23aivv sylbir syl2anb ster
      ) ABCFAGZBGZFHZUHUIDGZIZDJUIUHFHZUHUIDBKZLULUMDULUIUHUKMZIUMUHUIUKNUIUHUO
      UNOPQRUHUIEGZIZEJZUICGZUKIZDJZUHUSFHZUJUIUSFHURVASUQUTSZDJEJVBUQUTEDTVCVB
      EDVCUHUSUKUPUAZIZVBUTUQVEUHUIUSUKUPUBUCUHUSVDAKOPUDUEUHUIEUNLUIUSDCKLUFUG
      $.
      $([19-Mar-98]$)
  $}

  $( Strict dominance is irreflexive.  Theorem 21(i) of [Suppes] p. 97. $)
  sdomirr $p |- -. A ~< A $=
    ( cvv wcel csdm wbr wn cen sdomnen enrefg nsyl3 relsdom elbrexi con3
    pm2.61i ) ABCZAADEZFPAAGEOAAHABIJPOAADKLMN $.
    $([4-Jun-98]$)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Symmetry of equinumerosity.  Theorem 2 of [Suppes] p. 92. $)
    ensymg $p |- ( B e. C -> ( A ~~ B -> B ~~ A ) ) $=
      ( vx vy wcel cen wbr cvv wi relen elbrexi cv wceq breq1 breq2 bi2imd
      visset ener ersym vtocl2i exp syl pm2.43b ) BCFZABGHZBAGHZUFAIFZUEUFUGJZJ
      ABGKLUHUEUIDMZEMZGHZUKUJGHZJAUKGHZUKAGHZJUIDEABICUJANULUNUMUOUJAUKGOUJAUK
      GPQUKBNUNUFUOUGUKBAGPUKBAGOQUJUKGDRERSTUAUBUCUD $.
      $([28-Oct-03]$)
  $}

  ${
    $d x y z f g h A $.  $d x y z f g B $.  $d x y z f g h C $.
        $( ` x y z f g h ` are dummy. $)
    $( Transitivity of equinumerosity.  Theorem 3 of [Suppes] p. 92. $)
    entr $p |- ( ( A ~~ B /\ B ~~ C ) -> A ~~ C ) $=
      ( vx vy vz cen relen cv visset ener ertr cvv wcel wbr enrefg ax-mp
      vtoclrbr ) DEFABCGHDIZEIFIGDJZEJFJKLSMNSSGOTSMPQR $.
      $([9-Jun-98]$)

    $( Transitivity of dominance relation.  Theorem 17 of [Suppes] p. 94. $)
    domtr $p |- ( ( A ~<_ B /\ B ~<_ C ) -> A ~<_ C ) $=
      ( vx vy vz vg vf cdom reldom cv wf1 wex wbr wa eeanv ccom f1co ancoms
      visset f1dom syl 19.23aivv sylbir brdom syl2anb cvv wcel domrefg ax-mp
      vtoclrbr ) DEFABCIJDKZEKZGKZLZGMZUMFKZHKZLZHMZULUQINZULUMINUMUQINUPUTOUOU
      SOZHMGMVAUOUSGHPVBVAGHVBULUQURUNQZLZVAUSUOVDULUMUQURUNRSULUQVCDTZUAUBUCUD
      ULUMGETUEUMUQHFTUEUFULUGUHULULINVEULUGUIUJUK $.
      $([4-Jun-98]$)
  $}

  $( Transitivity of equinumerosity and dominance. $)
  endomtr $p |- ( ( A ~~ B /\ B ~<_ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan ) ABDEBCDEACDEABFEABCGABHI $.
    $([7-Jun-98]$)

  $( Transitivity of dominance and equinumerosity. $)
  domentr $p |- ( ( A ~<_ B /\ B ~~ C ) -> A ~<_ C ) $=
    ( cdom wbr cen domtr endom sylan5 ) ABDEBCDEACDEBCFEABCGBCHI $.
    $([7-Jun-98]$)

  ${
    $d f A $.  $( ` f ` is dummy. $)
    $( The empty set is equinumerous only to itself.  Exercise 1 of
       [TakeutiZaring] p. 88. $)
    en0 $p |- ( A ~~ (/) <-> A = (/) ) $=
      ( vf c0 cen wbr wceq cv wf1o wex zfnull bren ccnv f1ocnv f1o0 pm3.27bd
      syl 19.23aiv sylbi cvv wcel enrefg ax-mp breq1 mpbiri impbi ) ACDEZACFZUF
      ACBGZHZBIUGACBJKUIUGBUICAUHLZHZUGACUHMUKUJCFUGAUJNOPQRUGUFCCDEZCSTULJCSUA
      UBACCDUCUDUE $.
      $([27-May-98]$)
  $}

  ${
    $d f A $.
    ensn1.1 $e |- A e. V $.
    $( A singleton is equinumerous to ordinal ` 1o ` . $)
    ensn1 $p |- { A } ~~ 1o $=
      ( vf csn c0 c1o cen wbr cv wf1o wex cop zfnull f1osn snex f1oeq1 cla4ev
      ax-mp p0ex bren mpbir df1o2 breqtrr ) ADZEDZFGUDUEGHUDUECIZJZCKZUDUEAELZD
      ZJZUHAEBMNUGUKCUJUIOUDUEUFUJPQRUDUECSTUAUBUC $.
      $([4-Nov-02]$)
  $}

  ${
    $d f y z w A $.  $d f y z w B $.
    mapsnen.1 $e |- A e. V $.
    mapsnen.2 $e |- B e. V $.
    $( Set exponentiation to a singleton exponent is equinumerous to its base.
       Exercise 4.43 of [Mendelson] p. 255. $)
    mapsnen $p |- ( A ^m { B } ) ~~ A $=
      ( vz vw vy csn cm co cv cfv cop oprex cvv wcel fvex a1i snex wceq wa wex
      weq wrex mapsn cleqabi biran r19.41v bitr4 df-rex bitr fveq1 visset fvsn
      syl6eq cleq2d eqcomb syl6bb pm5.32i bilan anass ancom biex eleq1 opeq2
      sneq syl bi2and ceqsexv 3bitr en2 ) EFABHZIJZABEKZLZBFKZMZHZAVLINVOOPVNVM
      PZBVNQRVROPVPAPZVQSRVSVPVOTZUAZGKZAPZVNBWCMZHZTZWAUAZUAZGUBZGFUCZWDWGUAZU
      AZGUBVTVNVRTZUAZWBWHGAUDZWJWBWGGAUDZWAUAWPVSWQWAWQEVMGABECDUEUFUGWGWAGAUH
      UIWHGAUJUKWIWMGWIWLWKUAZWMWIWDWGWKUAZUAWRWHWSWDWGWAWKWGWAFGUCWKWGVOWCVPWG
      VOBWFLWCBVNWFULBWCDGUMUNUOUPFGUQURUSUTWDWGWKVAUIWLWKVBUKVCWLWOGVPFUMWKWDV
      TWGWNWCVPAVDWKWFVRVNWKWEVQTWFVRTWCVPBVEWEVQVFVGUPVHVIVJVK $.
      $([?]$)
  $}

  ${
    $d x y A $.
    map1.1 $e |- A e. V $.
    $( Set exponentiation:  ordinal 1 to any set is equinumerous to ordinal 1.
       Exercise 4.42(b) of [Mendelson] p. 255. $)
    map1 $p |- ( 1o ^m A ) ~~ 1o $=
      ( vx vy c1o cm co c0 csn cxp oprex cvv wcel cv zfnull a1i p0ex xpex wceq
      wa ancom wf df1o2 opreq1i eleq2i elmap bitr fconst2 bitr2 elsn bi2an
      en2 ) CDEAFGZEHAHIZJZEAFKHLMCNZUMMZOPUOLMDNZEMZAUNBQRPUSUPUOSZTUTUSTUQURH
      SZTUSUTUAUTUQUSVAUQAUNUPUBZUTUQUPUNAFGZMVBUMVCUPEUNAFUCUDUEUNAUPQBUFUGAHU
      POUHUIUSURUNMVAEUNURUCUEDHUJUGUKUIUL $.
      $([?]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Two singletons are equinumerous. $)
    en2sn $p |- ( ( A e. C /\ B e. D ) -> { A } ~~ { B } ) $=
      ( vx csn c1o cen wbr wcel entr cv wceq sneq breq1d visset ensn1 vtoclg
      con0 wi 1o ensymg ax-mp syl syl2an ) AFZGHIZGBFZHIZUFUHHIACJBDJZUFGUHKELZ
      FZGHIZUGEACUKAMULUFGHUKANOUKEPQZRUJUHGHIZUIUMUOEBDUKBMULUHGHUKBNOUNRGSJUO
      UITUAUHGSUBUCUDUE $.
      $([9-Nov-03]$)
  $}

  ${
    $d x A $.
    $( A singleton is finite. $)
    snfi $p |- E. x e. om { A } ~~ x $=
      ( cvv wcel csn cv cen wbr com wrex c1o wceq sneq breq1d visset ensn1
      vtoclg 1nn breq2 rcla4ev mpan syl wn c0 snprc en0 peano1 sylbir sylbi
      pm2.61i ) BCDZBEZAFZGHZAIJZUKULKGHZUOUMEZKGHUPABCUMBLUQULKGUMBMNUMAOPQKID
      UPUORUNUPAKIUMKULGSTUAUBUKUCULUDLZUOBUEURULUDGHZUOULUFUDIDUSUOUGUNUSAUDIU
      MUDULGSTUAUHUIUJ $.
      $([4-Nov-02]$)
  $}


  ${
    $d f g h A $.  $d f g h B $.  $d f g h C $.  $d f g h D $.
        $( ` f g h ` are dummy. $)
    $( Equinumerosity of union of disjoint sets.  Theorem 4 of [Suppes]
       p. 92. $)
    unen $p |- ( ( ( A ~~ B /\ C ~~ D ) /\
  ( ( A i^i C ) = (/) /\ ( B i^i D ) = (/) ) ) -> ( A u. C ) ~~ ( B u. D ) ) $=
      ( vf vg vh cun cvv wcel cen wbr wa cin c0 wceq wi cv wf1o wex wb unexbg
      breng bi2anan9 sylbir f1oun visset unex f1oeq1 cla4ev syl syl5bir exp3a
      19.23advv eeanv syl5ibr sylbid imp3a wn brprc relen elbrexi im2an sylib
      enrefg adantrd pm2.61i ) BDHZIJZABKLZCDKLZMZACNOPBDNOPMZMACHZVHKLZQVIVLVM
      VOVIVLABERZSZETZCDFRZSZFTZMZVMVOQZVIBIJZDIJZMVLWBUABDUBWDVJVRWEVKWAABIEUC
      CDIFUCUDUEVIVQVTMZFTETWCWBVIWFWCEFVIWFVMVOVIVOVNVHGRZSZGTZWFVMMZVNVHIGUCW
      JVNVHVPVSHZSZWIABCDVPVSUFWHWLGWKVPVSEUGFUGUHVNVHWGWKUIUJUKULUMUNVQVTEFUOU
      PUQURVIUSZVLVOVMWMVOVNVNKLZVLVNVHKUTVLVNIJZWNVLAIJZCIJZMWOVJWPVKWQABKVAVB
      CDKVAVBVCACUBVDVNIVEUKULVFVG $.
      $([11-Jun-98]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    xpsnen.1 $e |- A e. V $.
    xpsnen.2 $e |- B e. V $.
    $( A set is equinumerous to its cross-product with a singleton.
       Proposition 4.22(c) of [Mendelson] p. 254. $)
    xpsnen $p |- ( A X. { B } ) ~~ A $=
      ( vy vx vz csn cxp cv cint cop snex xpex wcel wceq wa wex cvv elxp
      visset inteq inteqd op1st syl6eq eleq1d mpbiri adantr 19.23aivv sylbi
      opex a1i wb eleq1 mpbii opeq1 cleq2d bi2and ceqsexgv ancom anass elsn
      biran 3bitr3 biex opeq2 birand ceqsexv syl6req pm4.71ri bitr 3bitr
      syl5bb syl pm5.32ri pm4.71i bitr2 en2 ) EFABHZIZAEJZKZKZFJZBLZAVSCBMNWAVT
      OZWAWDGJZLZPZWDAOZWGVSOZQZQZGRZFRZWCSOZFGWAAVSTZWMWPFGWIWPWLWIWPWDSOZFUAZ
      WIWCWDSWIWCWHKZKWDWIWBWTWAWHUBUCWDWGWSUDUEUFUGUHUIUJWESOWJWDBUKULWFWDWCPZ
      QWAWCBLZPZWCAOZQZXAQZWAWEPZWJQZWJXGQXAWFXEXAWPWFXEUMXAWRWPWSWDWCSUNUOWPXA
      XHQZFRZXEWFXHXEFWCSXAXGXCWJXDXAWEXBWAWDWCBUPUQWDWCAUNURZUSWFWOXJWQWNXIFWN
      WGBPZWIWJQZQZGRXHXIWMXNGXMWKQWKXMQWMXNXMWKUTWIWJWKVAWKXLXMGBVBVCVDVEXMXHG
      BDXLWIXGWJXLWHWEWAWGBWDVFUQVGVHXHXAXGQZWJQXIXGXOWJXGXAXGWCWEKZKWDXGWBXPWA
      WEUBUCWDBWSUDVIZVJVCXAXGWJVAVKVLVEVKVMVNVOXHXHXAQXFXHXAXGXAWJXQUHVPXAXHXE
      XKVOVQXGWJUTVLVR $.
      $([?]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    xpcomen.1 $e |- A e. V $.
    xpcomen.2 $e |- B e. V $.
    $( Commutative law for equinumerosity of cross product.  Proposition
       4.22(d) of [Mendelson] p. 254. $)
    xpcomen $p |- ( A X. B ) ~~ ( B X. A ) $=
      ( vx vy vz vw cxp cv csn ccnv cuni xpex cvv wcel snex cnvex zfun2 a1i
      cop wceq wa wex sneq cnveq syl visset cnvsn syl6eq unieqd opex unisn
      syl6req cleq2tr ancom bi2an an23 3bitr4 bi2ex 19.41vv 3bitr3 elxp biran
      19.11 bitr en2 ) EFABIZBAIZEJZKZLZMZFJZKZLZMZABCDNVMOPVJVHPZVLVKVJQRSTVQO
      PVNVIPZVPVOVNQRSTVJGJZHJZUAZUBZVTAPZWABPZUCZUCZHUDGUDZVNVMUBZUCZVNWAVTUAZ
      UBZWEWDUCZUCZHUDGUDZVJVQUBZUCZVRWIUCVSWPUCWGWIUCZHUDGUDWNWPUCZHUDGUDWJWQW
      RWSGHWCWIUCZWFUCWLWPUCZWMUCWRWSWTXAWFWMVJVNWBWKVMVQWCVMWKKZMWKWCVLXBWCVLW
      BKZLZXBWCVKXCUBVLXDUBVJWBUEVKXCUFUGVTWAGUHZHUHZUIUJUKWKWAVTULUMUNWLVQXCMW
      BWLVPXCWLVPXBLZXCWLVOXBUBVPXGUBVNWKUEVOXBUFUGWAVTXFXEUIUJUKWBVTWAULUMUNUO
      WDWEUPUQWCWFWIURWLWMWPURUSUTWGWIGHVAWNWPGHVAVBVRWHWIGHVJABVCVDVSWOWPVSWNG
      UDHUDWOHGVNBAVCWNHGVEVFVDUSVG $.
      $([?]$)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    xpassen.1 $e |- A e. V $.
    xpassen.2 $e |- B e. V $.
    xpassen.3 $e |- C e. V $.
    $( Associative law for equinumerosity of cross product.  Proposition
       4.22(e) of [Mendelson] p. 254. $)
    xpassen $p |- ( ( A X. B ) X. C ) ~~ ( A X. ( B X. C ) ) $=
      ( vx vy vz vw vv vu cxp cv csn cdm cuni crn cop xpex cvv wcel opex a1i
      wceq wa wex opeq12 sneq dmeqd unieqd sneqd op1sta sneqi dmeqi unieqi
      visset eqtr syl6req rneqd rneqi op2nda sylan13 cleq2tr anass bi2an an23
      3bitr4 biex 19.41v 3bitr3 bi2ex 19.41vv elxp 19.11 biran an12 exrot4
      opeq1 cleq2d birand biland ceqsexv 3bitr bi3ex bilan 19.42vv bitr bitr3
      exrot3 opeq2 en2 ) GHABMZCMZABCMZMZGNZOZPZQZOZPZQZXARZQZWRRZQZSZSZHNZOZPZ
      QZXKRZQZOZPZQZSZXPRZQZSZWMCABDETFTXIUAUBWQWNUBZXCXHUCUDYBUAUBXJWPUBZXSYAU
      CUDWQINZJNZSZKNZSZUEZYEAUBZYFBUBZUFZYHCUBZUFZUFZKUGZJUGIUGZXJXIUEZUFZXJYE
      YFYHSZSZUEZYKYLYNUFZUFZUFZKUGZJUGZIUGZWQYBUEZUFZYCYSUFYDUVJUFYQYSUFZJUGIU
      GUVGUVJUFZJUGIUGYTUVKUVLUVMIJYPYSUFZKUGUVFUVJUFZKUGUVLUVMUVNUVOKYJYSUFZYO
      UFUVCUVJUFZUVEUFUVNUVOUVPUVQYOUVEWQXJYIUVBXIYBYEXCUEUVAXHUEZUVBXIUEYJYEUV
      AXCXHUHYJXCYIOZPZQZOZPZQZYEYJXBUWCYJXAUWBYJWTUWAYJWSUVTYJWRUVSWQYIUIZUJUK
      ULZUJUKUWDYGOZPZQYEUWCUWHUWBUWGUWAYGYGYHYEYFUCZUMUNZUOUPYEYFIUQZUMURUSYFX
      EUEYHXGUEUVRYJYFYHXEXGUHYJXEUWBRZQZYFYJXDUWLYJXAUWBUWFUTUKUWMUWGRZQYFUWLU
      WNUWBUWGUWJVAUPYEYFUWKJUQZVBURUSYJXGUVSRZQYHYJXFUWPYJWRUVSUWEUTUKYGYHUWIK
      UQZVBUSVCVCYGXSUEZYHYAUEYIYBUEUVCYGYHXSYAUHYEXMUEYFXRUEUWRUVCYEYFXMXRUHUV
      CXMUVBOZPZQYEUVCXLUWTUVCXKUWSXJUVBUIZUJUKYEUVAUWKUMUSUVCXRUWSRZQZOZPZQZYF
      UVCXQUXEUVCXPUXDUVCXOUXCUVCXNUXBUVCXKUWSUXAUTUKULZUJUKUXFUVAOZPZQYFUXEUXI
      UXDUXHUXCUVAYEUVAUWKYFYHUCZVBUNZUOUPYFYHUWOUMURUSVCUVCYAUXDRZQZYHUVCXTUXL
      UVCXPUXDUXGUTUKUXMUXHRZQYHUXLUXNUXDUXHUXKVAUPYFYHUWOUWQVBURUSVCVDYKYLYNVE
      VFYJYOYSVGUVCUVEUVJVGVHVIYPYSKVJUVFUVJKVJVKVLYQYSIJVMUVGUVJIJVMVKYCYRYSYC
      WQLNZYHSZUEZUXOWMUBZYNUFUFZKUGLUGUXSLUGKUGZYRLKWQWMCVNUXSLKVOUXTUXOYGUEZY
      MUFZUXQYNUFZUFZJUGIUGZLUGKUGUYDLUGZKUGJUGIUGYRUXSUYEKLUXRUYCUFUYBJUGIUGZU
      YCUFUXSUYEUXRUYGUYCIJUXOABVNVPUXQUXRYNVQUYBUYCIJVMVHVLUYDKLIJVRUYFYPIJKUY
      FUYAYMUYCUFZUFZLUGYMYJYNUFZUFZYPUYDUYILUYAYMUYCVEVIUYHUYKLYGUWIUYAUYCUYJY
      MUYAUXQYJYNUYAUXPYIWQUXOYGYHVSVTWAWBWCYMYJYNVQWDWEWDWDVPYDUVIUVJYDXJYEUXO
      SZUEZYKUXOWOUBZUFUFZLUGZIUGUVIILXJAWOVNUYPUVHIUYPUXOUVAUEZUYMUVEUFZUFZKUG
      JUGZLUGUYSLUGZKUGJUGUVHUYOUYTLUYMYKUFZUYNUFVUBUYQUVDUFZKUGJUGZUFZUYOUYTUY
      NVUDVUBJKUXOBCVNWFUYMYKUYNVEVUEVUBVUCUFZKUGJUGUYTVUBVUCJKWGVUFUYSJKVUFUYQ
      VUBUVDUFZUFUYSVUBUYQUVDVQVUGUYRUYQUYMYKUVDVEWFWHVLWIVKVIUYSLJKWJVUAUVFJKU
      YRUVFLUVAUXJUYQUYMUVCUVEUYQUYLUVBXJUXOUVAYEWKVTWAWCVLWDVIWHVPVHWL $.
      $([?]$)
  $}

  ${
    $d x y z w v u A $.
    pw2en.1 $e |- A e. V $.
    $( The power set of a set is equinumerous to set exponentiation with a base
       of ordinal 2.  Proposition 10.44 of [TakeutiZaring] p. 96. $)
    pw2en $p |- P~ A ~~ ( 2o ^m A ) $=
      ( vx vy vz vw vv vu cpw c2o cm co cv wcel c0 wceq wel wa cab copab ccnv
      csn cima zfpowcl cvv wmo moeq a1i funopabex visset cnvex imaexg ax-mp
      wss cin sseqin2 biimp cleq1d cop eleq2 p0ex elimasn opelcnv bitr syl5bb
      wb wal cleq2ab df-sn cleq1i iba 19.21aiv zfnull cleq1 birand bi2bid
      cla4v wi cleqid a1bi pm4.71 sylibr impbi 3bitr4r bilan elin weq eleq1
      biland biabdv cleq2d bi2and opelopab 3bitr4 syl6rbbr cleqrd syl5bir comm
      sseq1 crn imassrn cdm wfn weu eqeltrr pm3.26 ssab2i ssexi eueq1 fnopab
      fneq1 mpbiri fndm syl dfdm4 syl5eqr sseq2d mpbii impbid elpw pm5.32ri
      ancom wf wral cpr pri2 df2o2 eleqtrr elsn biabrdv eleq1d wn pri1 wex
      noel3 pm3.27 19.23aiv sylbi con1 pm2.61i rgen fopab2 mpbi feq1 cfv
      syl6bb fnfvop ffn sylan bicomd sylan9bb biimpa adantl eqtrd exp wo ffvrn
      eleq2i fvex elpr sylib ord sylibrd pm2.15d imp eqtr4d pm2.61d fvopab2
      mpan2 ad2antrr exp32 r19.21aiv ax-17 hbopab1 cleqfvf biantrur syl6bbr
      mpbird con0 2o elisseti elmap 3bitr en2 ) CDAIZJAKLZEMZANZFMZGMZOPZECQZRZ
      GSZPZRZEFTZDMZUAZOUBZUBZUCZABUDUXQUENCMZUXENZUXOEFABUXOFUFUXHFUXNUGUHUIUH
      UYBUENZUXRUXFNZUXSUENUYEUXRDUJUKUXSUYAUEULUMUHUYDUXRUXQPZRUYCUYBPZUYGRUYG
      UYHRUYFUYHRUYGUYDUYHUYGUYCAUNZUYHUYDUYGUYIUYHUYIUYGUYHUYIUYHAUYCUOZUYBPUY
      GUYIUYCUYJUYBUYIUYCUYJPUYCAUPUQURUYGHUYJUYBUYGHMZUYBNZUYKUXTUSZUXQNZUYKUY
      JNZUYGUYMUXRNZUYNUYLUXRUXQUYMUTUYLUXTUYKUSUXSNUYPUXSUXTUYKVAHUJZVBUXTUYKU
      XRVAUYQVCVDVEUYKANZHCQZRUYRUXTUXKUYSRZGSZPZRZUYOUYNUYSVUBUYRUXKGSZVUAPUXK
      UYTVFZGVGZVUBUYSUXKUYTGVHUXTVUDVUAGOVIZVJUYSVUFUYSVUEGUYSUXKVKVLVUFOOPZVU
      HUYSRZVFZUYSVUEVUJGOVMUXKUXKVUHUYTVUIUXJOOVNZUXKUXKVUHUYSVUKVOVPVQUYSVUHU
      YSVRVUJVUHUYSOVSVTVUHUYSWAVDWBWCWDWEUYKAUYCWFUXPUYRUXIVUAPZRVUCEFUYKUXTUY
      QVAEHWGZUXHUYRUXOVULUXGUYKAWHVUMUXNVUAUXIVUMUXMUYTGVUMUXLUYSUXKUXGUYKUYCW
      HWIWJWKWLUXIUXTPVULVUBUYRUXIUXTVUAVNWIWMWNWOWPWQWRUYHUYGUYIUYHUYIUYBAUNZU
      YGUYCUYBAWSUYGUYBUXSWTZUNVUNUXSUYAXAUYGVUOAUYBUYGUXRXBZAVUOUYGUXRAXCZVUPA
      PUYGVUQUXQAXCZUXOEFAUXQUXOFXDUXHFUXNUXNVUDUXTVUDUEVUGVAXEUXMUXKGUXKUXLXFX
      GXHZXIUHUXQVSZXJZAUXRUXQXKXLAUXRXMXNUXRXOXPXQXRWQWRXSUYCACUJXTVEYAUYHUYGY
      BUYHUYGUYFUYHUYGAJUXRYCZUYFUYHUYGVVBUYGVVBVRUYHUYGVVBAJUXQYCZUXNJNZEAYDVV
      CVVDEAVVDUXHUXLVVDUXLUXTJNVVDUXTOUXTYEZJOUXTVAYFYGYHUXLUXTUXNJUXLUXMGUXTU
      XLUXKUXMUXJUXTNUXLUXKVKGOYIVEYJZYKXRUXLYLZVVDOJNOVVEJOUXTVMYMYGYHVVGUXNOJ
      UXNOPZUXLVVHYLUXMGYNUXLUXMGYOUXMUXLGUXKUXLYPYQYRYSZYKXLYTUHUVAEFAJUXNUXQV
      UTUVBUVCAJUXRUXQUVDXLUHUYHVVBUYGUYHVVBRZUYGUXGUXRUVEZUXGUXQUVEZPZEAYDZVVJ
      VVMEAUYHVVBUXHVVMVRUYHVVBUXHVVMUYHVVBUXHRZRZVVKUXNVVLVVPUXLVVKUXNPZVVPUXL
      VVQVVPUXLRVVKUXTUXNVVPUXLVVKUXTPZUYHUXLUXGUXTUSUXRNZVVOVVRUYHUXLUXGUYBNZV
      VSUYCUYBUXGUTVVTUXTUXGUSUXSNVVSUXSUXTUXGVAEUJZVBUXTUXGUXRVAVWAVCVDUVFVVOV
      VRVVSVUQUXHVVRVVSVFVVBAUXGUXTUXRVAUVGAJUXRUVHZUVIUVJUVKZUVLUXLUXTUXNPVVPV
      VFUVMUVNUVOVVPVVGVVQVVPVVGRVVKOUXNVVPVVGVVKOPZVVPVWDUXLVVPVWDYLZVVRUXLVVO
      VWEVVRVRUYHVVOVWDVVRVVOVVKJNZVWDVVRUVPZAJUXGUXRUVQVWFVVKVVENVWGJVVEVVKYGU
      VRVVKOUXTUXGUXRUVSUVTVDUWAUWBUVMVWCUWCUWDUWEVVGVVHVVPVVIUVMUWFUVOUWGUXHVV
      LUXNPZUYHVVBUXHUXNUENVWHVUSEFAUXNUEUWHUWIUWJUWFUWKUWEUWLVVBUYGVVNVFZUYHVV
      BVUQVWIVWBVUQUYGAAPZVVNRZVVNVUQVURUYGVWKVFVVAEHAAUXRUXQHDQEUWMUXPEFHUWNUW
      OUWIVWJVVNAVSUWPUWQXNUVMUWRUVOXSJAUXRJUWSUWTUXABUXBUWQYAUXCUXD $.
      $([?]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x D $.  $d x f $.  $d x g $.
    sbthlem.1 $e |- A e. V $.
    sbthlem.2 $e |- D = { x |
                   ( x (_ A /\ ( g " ( B \ ( f " x ) ) ) (_ ( A \ x ) ) } $.
    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem1 $p |- U. D (_ ( A \ ( g " ( B \ ( f " U. D ) ) ) ) $=
      ( cv wcel cuni cima cdif wss wi ssuniss sstr wa cleqabi ssconb biimprd
      exp difss mpi syl5 pm2.43d imp sylbi elssuni imass2 sscon 3syl syl13 mpg
      ) AIZDJZUOBFIZCEIZDKZLZMZLZMZNZOUSVCNAADVCPUOBUQCURUOLZMZLZMZNZVHVCNZVDUP
      UOVHVCQUPUOBNZVGBUOMZNZRZVIVNADHSVKVMVIVKVMVIVKVGBNZVMVIOZVMVKVOVPVKVORVI
      VMUOVGBTUAUBVMVLBNVOBUOUCVGVLBQUDUEUFUGUHUPVAVFNZVBVGNVJUPUOUSNVEUTNVQUOD
      UIUOUSURUJVEUTCUKULVAVFUQUJVBVGBUKULUMUN $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem2 $p |- ( ran g (_ A ->
                     ( A \ ( g " ( B \ ( f " U. D ) ) ) ) (_ U. D ) $=
      ( cv crn wss cuni cima cdif wcel wa cab sbthlem1 imass2 ax-mp sscon wb
      wi imassrn sstr difss ssconb mpan2 syl mpbiri jctil ssexi wceq sseq1
      difeq2 sseq2d imaeq2 difeq2d 3syl bitrd bi2and elab sylibr eleq2i
      elssuni ) FIZJZBKZBVFCEIZDLZMZNZMZNZDOZVNVJKVHVNAIZBKZVFCVIVPMZNZMZBVPNZK
      ZPZAQZOZVOVHVNBKZVFCVIVNMZNZMZBVNNZKZPZWEVHWKWFVHWKVNBWINKZWIVMKZWMWHVLKZ
      WNVKWGKZWOVJVNKWPABCDEFGHRVJVNVISTVKWGCUATWHVLVFSTWIVMBUATVHWIBKZWKWMUBZW
      IVGKVHWQUCVFWHUDWIVGBUETWQWFWRBVMUFZWIVNBUGUHUIUJWSUKWCWLAVNVNBGWSULVPVNU
      MZVQWFWBWKVPVNBUNWTWBVTWJKZWKWTWAWJVTVPVNBUOUPWTVSWHUMVTWIUMXAWKUBWTVRWGC
      VPVNVIUQURVSWHVFUQVTWIWJUNUSUTVAVBVCDWDVNHVDVCVNDVEUI $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem3 $p |- ( ran g (_ A ->
                  ( g " ( B \ ( f " U. D ) ) ) = ( A \ U. D ) ) $=
      ( cv crn wss cuni cima cdif wceq wi imassrn sstr ax-mp dfss4 sylib wa
      sbthlem2 sbthlem1 jctil eqss sylibr difeq2d eqtr4d ) FIZJZBKZUJCEIDLZMNZM
      ZBBUONZNZBUMNULUOBKZUOUQOUOUKKULURPUJUNQUOUKBRSUOBTUAULUMUPBULUMUPKZUPUMK
      ZUBUMUPOULUTUSABCDEFGHUCABCDEFGHUDUEUMUPUFUGUHUI $.
      $([22-Mar-98]$)

    $( Lemma for Schroder-Bernstein Theorem. $)
    sbthlem4 $p |- ( ( ( dom g = B /\ ran g (_ A ) /\ Fun `' g ) ->
                  ( `' g " ( A \ U. D ) ) = ( B \ ( f " U. D ) ) ) $=
      ( cv cdm wceq crn wss wa ccnv wfun cuni cima cdif cres difss sseq2
      mpbiri ssdmres sylib dfdm4 syl6eq funcnvres sbthlem3 reseq2 syl
      sylan9eqr rneq sylan9eq anassrs df-ima syl6reqr ) FIZJZCKZURLBMZNUROZPZNC
      EIDQZRZSZVBBVDSZTZLZVBVGRUTVAVCVFVIKUTVAVCNZVFURVFTZOZLZVIUTVFVKJZVMUTVFU
      SMZVFVNKUTVOVFCMCVEUAUSCVFUBUCVFURUDUEVKUFUGVJVLVHKVMVIKVCVAVLVBURVFRZTZV
      HVFURUHVAVPVGKVQVHKABCDEFGHUIVPVGVBUJUKULVLVHUMUKUNUOVBVGUPUQ $.
      $([27-Mar-98]$)

    ${
      $d x H $.
      sbthlem.3 $e |- H = ( ( f |` U. D ) u. ( `' g |` ( A \ U. D ) ) ) $.

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem5 $p |- ( ( dom f = A /\ ran g (_ A ) -> dom H = A ) $=
        ( cv cdm wceq crn wss wa cuni cdif cun cin cima sbthlem1 difss sstri
        sseq2 mpbiri df-ss sylib uneq1d imassrn sbthlem3 sseq1d mpbii uneq2d
        sylan9eq cres ccnv dmeq ax-mp dmun dmres uneq1i df-rn cleqcomi ineq2i
        eqtr uneq2i 3eqtr syl6reqr undif3 mpbi syl6eqr ) EKZLZBMZFKZNZBOZPZGLZD
        QZBWARZSZBVSWCWAVNTZWBVQTZSZVTVOVRWCWDWBSWFVOWAWDWBVOWAVNOZWAWDMVOWGWAB
        OZWABVPCVMWAUARZUAZRBABCDEFHIUBBWJUCUDZVNBWAUEUFWAVNUGUHUIVRWBWEWDVRWBV
        QOZWBWEMVRWJVQOWLVPWIUJVRWJWBVQABCDEFHIUKULUMWBVQUGUHUNUOVTVMWAUPZVPUQZ
        WBUPZSZLZWMLZWOLZSZWFGWPMVTWQMJGWPURUSWMWOUTWTWDWSSWFWRWDWSVMWAVAVBWSWE
        WDWSWBWNLZTWEWNWBVAXAVQWBVQXAVPVCVDVEVFVGVFVHVIWHBWCMWKWABVJVKVL $.
        $([22-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem6 $p |- ( ( ran f (_ B /\
               ( ( dom g = B /\ ran g (_ A ) /\ Fun `' g ) ) -> ran H = B ) $=
        ( cv crn wss cdm wceq wa ccnv wfun cuni cima cdif cun wi imassrn sstr
        ax-mp undif3 sylib cres sbthlem4 df-ima syl5reqr uneq2d rnun rneq
        uneq1i 3eqtr4 syl6eqr sylan9eq cleqcomd ) EKZLZCMZFKZNCOVDLBMPVDQZRPZPC
        GLZVCVFCVADSZTZCVIUAZUBZVGVCVICMZCVKOVIVBMVCVLUCVAVHUDVIVBCUEUFVICUGUHV
        FVKVIVEBVHUAZUIZLZUBZVGVFVJVOVIVFVEVMTVJVOABCDEFHIUJVEVMUKULUMVAVHUIZVN
        UBZLZVQLZVOUBVGVPVQVNUNGVROVGVSOJGVRUOUFVIVTVOVAVHUKUPUQURUSUT $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem7 $p |- ( ( Fun f /\ Fun `' g ) -> Fun H ) $=
        ( cv wfun ccnv wa cuni cres cdif cun cdm cin c0 wceq wss dmres inss1
        eqsstr ssrin ax-mp sslin sstri indif0 sseqtr ss0 funun mpan2 funres
        syl2an wb funeq sylibr ) EKZLZFKMZLZNVADOZPZVCBVEQZPZRZLZGLZVFLZVHLZVJV
        BVDVLVMNVFSZVHSZTZUAUBZVJVPUAUCVQVPVEVGTZUAVPVEVOTZVRVNVEUCVPVSUCVNVEVA
        SZTVEVAVEUDVEVTUEUFVNVEVOUGUHVOVGUCVSVRUCVOVGVCSZTVGVCVGUDVGWAUEUFVOVGV
        EUIUHUJVEBUKULVPUMUHVFVHUNUOVEVAUPVGVCUPUQGVIUBVKVJURJGVIUSUHUT $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem8 $p |- ( ( Fun `' f /\ ( ( ( Fun g /\ dom g = B ) /\ ran g (_ A )
                     /\ Fun `' g ) ) -> Fun `' H ) $=
        ( cv ccnv wfun cdm wceq wa crn wss cuni cres cdif cun cin c0 funun
        funres11 wrel funrel dfrel2 sylib funeq biimpd mpcom syl adantr
        ad2antll im2an cima sbthlem4 df-ima df-rn eqtr syl5eqr eqtr2 jctil
        ineq12 indif0 syl6eq pm3.27 imran sylan adantl sylan13 wb cnveq ax-mp
        cnvun sylibr ) EKZLMZFKZMZWANCOZPZWAQBRZPZWALZMZPZPZVSDSZTZLZWGBWKUAZTZ
        LZUBZMZGLZMZWMMZWPMZPWMNZWPNZUCZUDOZWRWJWMWPUEVTXAWIXBWKVSUFWDXBWEWHWBX
        BWCWBWGLZMZXBWAXGOZWBXHWBWAUGXIWAUHWAUIUJXIWBXHWAXGUKULUMWNWGUFUNUOUPUQ
        WIXFVTWCWEPZWHXFWFXJWHPZXEVSWKURZCXLUAZUCZUDXKXCXLOZXDXMOZPXEXNOXKXPXOX
        KWGWNURZXMXDABCDEFHIUSXQWOQXDWGWNUTWOVAVBVCXLWLQXCVSWKUTWLVAVDVEXCXLXDX
        MVFUNXLCVGVHWDWCWEWBWCVIVJVKVLVMWSWQOWTWRVNWSWLWOUBZLZWQGXROWSXSOJGXRVO
        VPWLWOVQVBWSWQUKVPVR $.
        $([27-Mar-98]$)

      $( Lemma for Schroder-Bernstein Theorem. $)
      sbthlem9 $p |- ( ( f : A -1-1-> B /\ g : B -1-1-> A ) ->
                    H : A -1-1-onto-> B ) $=
        ( cv wfun cdm wceq wa crn wss ccnv wf1 wf1o sbthlem7 sbthlem5 adantrl
        im2an an41r3s adantlr sbthlem8 adantll sbthlem6 df-rn syl5eqr pm3.27
        imran sylan5 jca wf df-f1 wfn df-f df-fn biran bitr bi2an f1o4 3imtr4
        ) EKZLZVFMBNZOZVFPCQZOZVFRLZOZFKZLZVNMCNZOZVNPBQZOZVNRLZOZOZGLZGMBNZOZG
        RZLZWFMZCNZOZOZBCVFSZCBVNSZOBCGTZWBWEWJVKWAWEVLVIWAWEVJVGVTVHVSWEVGVTOW
        CVHVSOWDABCDEFGHIJUAVHVRWDVQABCDEFGHIJUBUCUDUEUFUFWBWGWIVLWAWGVKABCDEFG
        HIJUGUHVKWAWIVLVJWAWIVIVJVPVROZVTOZWIWAVJWPOGPCWHABCDEFGHIJUIGUJUKVSWOV
        TVQVPVRVOVPULUMUMUNUHUFUOUOWLVMWMWAWLBCVFUPZVLOVMBCVFUQWQVKVLWQVFBURZVJ
        OVKBCVFUSWRVIVJVFBUTVAVBVAVBWMCBVNUPZVTOWACBVNUQWSVSVTWSVNCURZVROVSCBVN
        USWTVQVRVNCUTVAVBVAVBVCWNGBURZWFCURZOWKBCGVDXAWEXBWJGBUTWFCUTVCVBVE $.
        $([28-Mar-98]$)

      ${
        $d f g A $.  $d f g B $.
        sbthlem.4 $e |- B e. V $.
        $( Lemma for Schroder-Bernstein Theorem. $)
        sbthlem10 $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
          ( cdom wbr wa cv wf1 wex cen brdom bi2an eeanv bitr4 wf1o sbthlem9
          f1oen syl 19.23aivv sylbi ) BCLMZCBLMZNZBCEOPZCBFOPZNZFQEQZBCRMZUKULE
          QZUMFQZNUOUIUQUJURBCEKSCBFHSTULUMEFUAUBUNUPEFUNBCGUCUPABCDEFGHIJUDBCG
          HUEUFUGUH $.
          $([28-Mar-98]$)
      $}
    $}

  $}

  ${
    $d x y z w f g A $.  $d x y z w f g B $.  $( ` x y z w f g ` are dummy. $)
    $( Schroder-Bernstein Theorem.  Theorem 18 of [Suppes] p. 95.  This theorem
       states that if set ` A ` is smaller (has lower cardinality) than ` B `
       and vice-versa, then ` A ` and ` B ` are equinumerous (have the same
       cardinality).  The interesting thing is that this can be proved without
       invoking the Axiom of Choice, as we do here, but the proof as you can
       see is quite difficult.  (The theorem can be proved more easily if we
       allow AC.)  The main proof consists of lemmas ~ sbthlem1 through
       ~ sbthlem10 ; this final piece mainly changes bound variables to
       eliminate the hypotheses of ~ sbthlem10 . $)
    sbth $p |- ( ( A ~<_ B /\ B ~<_ A ) -> A ~~ B ) $=
      ( vz vw vx vy vg vf cdom wbr wa cen cvv wcel wi cv wceq breq1 breq2
      bi2and bi2imd wss cima cdif cab cuni cres ccnv cun visset weq sseq1 wb
      imaeq2 difeq2d 3syl difeq2 sseq2d bitrd cbvabv cleqid sbthlem10 vtocl2i
      reldom elbrexi syl2an pm2.43i ) ABIJZBAIJZKZABLJZAMNBMNVJVKOZVHVICPZDPZIJ
      ZVNVMIJZKZVMVNLJZOAVNIJZVNAIJZKZAVNLJZOVLCDABMMVMAQZVQWAVRWBWCVOVSVPVTVMA
      VNIRVMAVNISTVMAVNLRUAVNBQZWAVJWBVKWDVSVHVTVIVNBAISVNBAIRTVNBALSUAEVMVNFPZ
      VMUBZGPZVNHPZWEUCZUDZUCZVMWEUDZUBZKZFUEZHGWHWOUFZUGWGUHVMWPUDUGUIZCUJWNEP
      ZVMUBZWGVNWHWRUCZUDZUCZVMWRUDZUBZKFEFEUKZWFWSWMXDWEWRVMULXEWMXBWLUBZXDXEW
      JXAQWKXBQWMXFUMXEWIWTVNWEWRWHUNUOWJXAWGUNWKXBWLULUPXEWLXCXBWEWRVMUQURUSTU
      TWQVADUJVBVCABIVDVEBAIVDVEVFVG $.
      $([8-Jun-98]$)
  $}

  $( Schroder-Bernstein Theorem and its converse. $)
  sbthbg $p |- ( B e. C -> ( ( A ~<_ B /\ B ~<_ A ) <-> A ~~ B ) ) $=
    ( wcel cdom wbr wa cen wi sbth a1i endom ensymg syl6 jcad impbid ) BCDZABEF
    ZBAEFZGZABHFZTUAIQABJKQUARSUARIQABLKQUABAHFSABCMBALNOP $.
    $([9-Jan-04]$)

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Schroder-Bernstein Theorem in class form. $)
    sbthcl $p |- ~~ = ( ~<_ i^i `' ~<_ ) $=
      ( vx vy cen cdom ccnv cin relen wrel reldom relin ax-mp cv wbr wa cop
      wcel cvv wb visset sbthbg df-br opelcnv bitr4 bi2an elin 3bitr3r
      cleqreli ) ABCDDEZFZGDHUIHIDUHJKALZBLZDMZUKUJDMZNZUJUKCMZUJUKOZUIPZUPCPUK
      QPUNUORBSZUJUKQTKUNUPDPZUPUHPZNUQULUSUMUTUJUKDUAUMUKUJODPUTUKUJDUAUJUKDAS
      URUBUCUDUPDUHUEUCUJUKCUAUFUG $.
      $([28-Mar-98]$)
  $}

  $( Alternate definition of strict dominance.  Compare Definition 3 of
     [Suppes] p. 97. $)
  dfsdom2 $p |- ~< = ( ~<_ \ `' ~<_ ) $=
    ( cdom cen cdif ccnv cin csdm sbthcl difeq2i df-sdom difin 3eqtr4 ) ABCAAAD
    ZEZCFALCBMAGHIALJK $.
    $([31-Mar-98]$)

  $( Strict dominance is not symmetric.  Theorem 21(ii) of [Suppes] p. 97. $)
  sdomnsym $p |- ( A ~< B -> -. B ~< A ) $=
    ( csdm wbr cen sdomnen cdom sbth sdomdom syl2an exp mtod ) ABCDZBACDZABEDZA
    BFMNOABGDBAGDOMNABHABIBAIJKL $.
    $([8-Jun-98]$)

  $( Theorem 22(i) of [Suppes] p. 97. $)
  domnsym $p |- ( A ~<_ B -> -. B ~< A ) $=
    ( cdom wbr csdm cen wo wn brdom2 sdomnsym cvv wcel ensymg pm2.16d relsdom
    elbrexi sdomnen syl13 con2 jaoi sylbi ) ABCDABEDZABFDZGBAEDZHZABIUBUEUCABJU
    DUCBKLZBAFDZHUCHUDUFUCUGABKMNBAEOPBAQRSTUA $.
    $([10-Jun-98]$)

  $( Any set dominates the empty set. $)
  0dom $p |- (/) ~<_ A $=
    ( c0 cvv wcel wss cdom wbr zfnull nullss ssdomg mp2 ) BCDBAEBAFGHAIBACJK $.
    $([10-Nov-03]$)

  $( The empty set does not strictly dominate any set. $)
  sdom0 $p |- -. A ~< (/) $=
    ( c0 cdom wbr csdm wn 0dom domnsym ax-mp ) BACDABEDFAGBAHI $.
    $([10-Nov-03]$)

  $( Transitivity of strict dominance and dominance.  Theorem 22(iii) of
     [Suppes] p. 97. $)
  sdomdomtr $p |- ( C e. D -> ( ( A ~< B /\ B ~<_ C ) -> A ~< C ) ) $=
    ( wcel csdm wbr cdom wa cen wn sdomnen ad2antrl wi domtr sdomdom sylan wo
    brdom2 df-or bitr sylib adantl ensymg endom syl6 sylan9r a1d jcad pm3.27
    sbth entr mt3d exp ) CDEZABFGZBCHGZIZACFGZUOURIZUSABJGZUPVAKUOUQABLMUTUSKZA
    CJGZCBJGZIVAUTVBVCVDURVBVCNZUOURACHGZVEABHGZUQVFUPABCOABPZQVFUSVCRVEACSUSVC
    TUAUBZUCUTVBCBHGZUQIVDUTVBVJUQUTVBCAHGZVGIVJUTVBVKVGURVBVCUOVKVIUOVCCAJGVKA
    CDUDCAUEUFUGUTVGVBUPVGUOUQVHMUHUICABOUFUTUQVBURUQUOUPUQUJUCUHUICBUKUFUIACBU
    LUFUMUN $.
    $([31-Oct-03]$)

  $( Transitivity of strict dominance and equinumerosity. $)
  sdomentr $p |- ( C e. D -> ( ( A ~< B /\ B ~~ C ) -> A ~< C ) ) $=
    ( wcel csdm wbr cdom cen sdomdomtr endom sylan5d ) CDEABFGBCHGACFGBCIGABCDJ
    BCKL $.
    $([8-Nov-03]$)

  $( Transitivity of equinumerosity and strict dominance. $)
  ensdomtr $p |- ( ( A ~~ B /\ B ~< C ) -> A ~< C ) $=
    ( cvv wcel cen wbr csdm wa wi cdom wn endomtr exp adantl ensymg imp entr
    syl pm2.16d im2and brsdom 3imtr4g imp3a relsdom elbrexi con3 pm2.21d
    adantld pm2.61i ) BDEZABFGZBCHGZIACHGZJUKULUMUNUKULUMUNJUKULIZBCKGZBCFGZLZI
    ACKGZACFGZLZIUMUNUOUPUSURVAULUPUSJUKULUPUSABCMNOUOUTUQUOBAFGZUTUQJUKULVBABD
    PQVBUTUQBACRNSTUABCUBACUBUCNUDUKLZUMUNULVCUMUNUMUKBCHUEUFUGUHUIUJ $.
    $([8-Nov-03]$)

  $( Strict dominance is transitive.  Theorem 21(iii) of [Suppes] p. 97. $)
  sdomtr $p |- ( ( A ~< B /\ B ~< C ) -> A ~< C ) $=
    ( csdm wbr cvv wcel wa wi wn sdomirr brprc mtbiri a3i cdom sdomdomtr
    sdomdom sylan5d syl anabsi7 ) ABDEZBCDEZACDEZUBCFGZUAUBHUCIUDUBUDJUBBBDEBKB
    CDLMNUDUABCOEUCUBABCFPBCQRST $.
    $([9-Jun-98]$)

  $( Transitivity of dominance and strict dominance.  Theorem 22(ii) of
     [Suppes] p. 97. $)
  domsdomtr $p |- ( ( A ~<_ B /\ B ~< C ) -> A ~< C ) $=
    ( cdom wbr csdm cen wo wi brdom2 sdomtr exp cvv wcel relsdom elbrexi wa wn
    endomtr adantl ensymg entr syl6 imp pm2.16d im2and brsdom 3imtr4g syl
    pm2.43b jaoi sylbi ) ABDEZBCFEZACFEZUMABFEZABGEZHUNUOIZABJUPURUQUPUNUOABCKL
    UQUNUOUNBMNZUQURIBCFOPUSUQURUSUQQZBCDEZBCGEZRZQACDEZACGEZRZQUNUOUTVAVDVCVFU
    QVAVDIUSUQVAVDABCSLTUTVEVBUSUQVEVBIZUSUQBAGEZVGABMUAVHVEVBBACUBLUCUDUEUFBCU
    GACUGUHLUIUJUKULUD $.
    $([10-Jun-98]$)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  enen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~~ C <-> B ~~ C ) ) $=
    ( wcel cen wbr wa wi ensymg imp entr exp syl adantll impbid ) BDEZABFGZHZAC
    FGZBCFGZSBAFGZTUAIQRUBABDJKUBTUABACLMNSUATRUATQABCLOMP $.
    $([?]$)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  enen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~~ A <-> C ~~ B ) ) $=
    ( wcel cen wbr wa wi entr exp comm adantl ensymg imp syl impbid ) BDEZABFGZ
    HZCAFGZCBFGZSUAUBIRUASUBUASUBCABJKLMTBAFGZUBUAIRSUCABDNOUBUCUAUBUCUACBAJKLP
    Q $.
    $([?]$)

  $( Equality-like theorem for equinumerosity and dominance. $)
  domen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~<_ C <-> B ~<_ C ) ) $=
    ( wcel cen wbr wa cdom wi ensymg imp endomtr exp syl adantl impbid ) BDEZAB
    FGZHZACIGZBCIGZTBAFGZUAUBJRSUCABDKLUCUAUBBACMNOSUBUAJRSUBUAABCMNPQ $.
    $([?]$)

  $( Equality-like theorem for equinumerosity and dominance. $)
  domen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~<_ A <-> C ~<_ B ) ) $=
    ( wcel cen wbr wa cdom wi domentr exp comm adantl ensymg imp syl impbid )
    BDEZABFGZHZCAIGZCBIGZTUBUCJSUBTUCUBTUCCABKLMNUABAFGZUCUBJSTUDABDOPUCUDUBUCU
    DUBCBAKLMQR $.
    $([?]$)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  sdomen1 $p |- ( ( B e. D /\ A ~~ B ) -> ( A ~< C <-> B ~< C ) ) $=
    ( wcel cen wbr wa csdm wi ensymg imp ensdomtr exp syl adantll impbid ) BDEZ
    ABFGZHZACIGZBCIGZTBAFGZUAUBJRSUCABDKLUCUAUBBACMNOTUBUASUBUARABCMPNQ $.
    $([8-Nov-03]$)

  $( Equality-like theorem for equinumerosity and strict dominance. $)
  sdomen2 $p |- ( ( B e. D /\ A ~~ B ) -> ( C ~< A <-> C ~< B ) ) $=
    ( cvv wcel cen wbr wa csdm wb wi sdomentr exp3a com23 imp adantll ensymg
    syl9r imp31 impbid exp31 imp3a wn relen elbrexi con3 pm2.21d adantld
    pm2.61i ) AEFZBDFZABGHZICAJHZCBJHZKZLUKULUMUPUKULUMUPUKULIUMIUNUOULUMUNUOLZ
    UKULUMUQULUNUMUOULUNUMUOCABDMNOPQUKULUMUOUNLZULUMBAGHZUKURABDRUKUOUSUNUKUOU
    SUNCBAEMNOSTUAUBUCUKUDZUMUPULUTUMUPUMUKABGUEUFUGUHUIUJ $.
    $([?]$)

  ${
    $d x y z f A $.  $( ` x y z f ` are dummy. $)
    canth2.1 $e |- A e. V $.
    $( Cantor's Theorem.  No set is equinumerous to its power set.
       Specifically, any set has a cardinality (size) strictly less than the
       cardinality of its power set.  For example, the cardinality of real
       numbers is the same as the cardinality of the power set of integers,
       so real numbers cannot be put into a one-to-one correspondence
       with integers. Theorem 23 of [Suppes] p. 97.  For the function version,
       see ~ canth . $)
    canth2 $p |- A ~< P~ A $=
      ( vx vy vf cpw csdm wbr cdom cen wn wa cvv wcel cv csn visset snelpw
      biimp wceq weq wb sneqr sneq impbi a1i dom2 ax-mp wf1o wex wfo canth wf1
      df-f1o pm3.27bd mto nex zfpowcl bren mtbir pm3.2i brsdom mpbir ) AAFZGHAV
      DIHZAVDJHZKZLVEVGAMNVEBCDAVDCOZPZDOZPZMVHANZVIVDNVHACQZRSVIVKTZCDUAZUBVLV
      JANLVNVOVHVJVMUCVHVJUDUEUFUGUHVFAVDEOZUIZEUJVQEVQAVDVPUKZAVPBULVQAVDVPUMV
      RAVDVPUNUOUPUQAVDEABURUSUTVAAVDVBVC $.
      $([1-Apr-98]$)
  $}

  ${
    $d x A $.
    $( Cantor's theorem with the sethood requirement expressed as an
       antecedent.  Theorem 23 of [Suppes] p. 97.  $)
    canth2g $p |- ( A e. B -> A ~< P~ A ) $=
      ( vx cv cpw csdm wbr wceq wb pweq breq12 mpian visset canth2 vtoclg ) CDZ
      PEZFGZAAEZFGZCABPAHQSHRTIPAJPAQSFKLPCMNO $.
      $([11-Nov-03]$)
  $}

  ${
    $d f g x y z w v A $. $d f g x y z w v B $. $d f g x y z w v C $.
    $d f g x y z w v D $. $d z w v H $.
    mapenlem.1 $e |- A e. V $.
    mapenlem.2 $e |- B e. V $.
    mapenlem.3 $e |- C e. V $.
    mapenlem.4 $e |- D e. V $.
    mapenlem.5 $e |- H =
         { <. x , y >. | ( x e. ( A ^m C ) /\ y = ( ( f o. x ) o. `' g ) ) } $.

    mapenlem1 $p |- ( ( ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) /\ z :
    C --> A ) /\ v e. C ) -> ( ( H ` z ) ` ( g ` v ) ) = ( f ` ( z ` v ) ) ) $=
      ( cv wf1o wa wf wcel cfv ccom ccnv wceq cm co elmap weq coeq2 coeq1d
      visset coex cnvex opabval2 sylbir fveq1d ad2antlr cid cres f1ococnv1
      coeq2d fcoi1 sylan9eqr fco f1of sylan an1rs coass syl5eq adantr wi wfun
      fvco2 exp funco f1ofun ffun syl2an wfo f1o3 pm3.27bd exp31 pm2.43d com23
      imp41 anasss adantlr anassrs 3eqtr3d eqtrd ) EFIQZRZGHJQZRZSZGECQZTZSZDQZ
      GUAZSZWTWNUBZWQKUBZUBZXCWLWQUCZWNUDZUCZUBZWTWQUBWLUBZWRXEXIUEWPXAWRXCXDXH
      WRWQEGUFUGZUAXDXHUEEGWQLNUHABWQWLAQZUCZXGUCXHXKKACUIXMXFXGXLWQWLUJUKPXFXG
      WLWQIULCULUMWNJULUNUMUOUPUQURXBWTXHWNUCZUBZWTXFUBZXIXJWSXOXPUEXAWSWTXNXFW
      SXFXGWNUCZUCZXFXNWMWRWOXRXFUEZGFXFTZWOXSWMWRSZWOXTXRXFUSGUTZUCXFWOXQYBXFG
      HWNVAVBGFXFVCVDEFWLTWRXTWMGEFWLWQVEEFWLVFVGVGVHXFXGWNVIVJUQVKWMWOWRXAXOXI
      UEZWMWRWOXAYCVLZWMWRWOYDVLYAWOYDYAWOWOYDXHVMZGHWNTZYDYAWOSWOYEYFSXAYCGHWT
      XHWNVNVOXFVMZXGVMZYEYAWOXFXGVPWLVMZWQVMYGWMWRWLWQVPEFWLVQZGEWQVRVSWOGHWNV
      TYHGHWNWAWBVSGHWNVFVSWCWDVOWEWFWPWRXAXPXJUEZWMWRXASZYKWOYIYLYKWMYIWRXAYKG
      EWTWLWQVNWGYJVGWHWIWJWK $.
      $([?]$)

    $( Lemma for mapping equinumerosity theorem. $)
    mapenlem $p |- ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) ->
             H : ( A ^m C ) -1-1-onto-> ( B ^m D ) ) $=
      ( vz vw vv cv wf1o wa cm co wf1 crn wceq wf cfv weq wi wral ccom ccnv
      wcel fco f1of sylan f1ocnv syl syl2an exp31 com23 imp elmap 3imtr4g
      r19.21aiv fopab2 sylib fveq1 adantl ad2antlr mapenlem1 adantrl exp43
      adantrd imp42 adantld 3eqtr3d wb f1fveq f1of1 ffvrn adantlr adantll jca
      anassrs mpbid exp wfn cleqfv cleqid biantrur syl6bbr ffn ad2antrl mpbird
      exp32 bi2an syl5ib exp3a r19.21adv f1fv sylibr wss frn wex an1rs coeq2
      coeq1d visset cnvex coex opabval2 cid cres f1ococnv2 fcoi2 sylan9eq
      coeq2d fcoi1 eqtrd coass coeq2i coeq1i eqtr2 3eqtr syl5eq eleq1 fveq2
      cleq1d bi2and cla4ev syl6 fvelrn 3syl sylibrd ssrdv eqss f1o5 ) CDGRZSZEF
      HRZSZTZCEUAUBZDFUAUBZIUCZIUDZUVEUEZTUVDUVEISUVCUVFUVHUVCUVDUVEIUFZORZIUGZ
      PRZIUGZUEZOPUHZUIZPUVDUJZOUVDUJZTUVFUVCUVIUVRUVCYSARZUKZUVAULZUKZUVEUMZAU
      VDUJUVIUVCUWCAUVDUVCECUVSUFZFDUWBUFZUVSUVDUMUWCYTUVBUWDUWEUIYTUWDUVBUWEYT
      UWDUVBUWEEDUVTUFZFEUWAUFZUWEYTUWDTUVBFEDUVTUWAUNCDYSUFUWDUWFYTECDYSUVSUNC
      DYSUOUPUVBFEUWASUWGEFUVAUQFEUWAUOURUSUTVAVBCEUVSJLVCDFUWBKMVCVDVEABUVDUVE
      UWBINVFVGZUVCUVQOUVDUVCUVJUVDUMZUVPPUVDUVCUWIUVLUVDUMZUVPUVCECUVJUFZECUVL
      UFZTZUVPUWIUWJTUVCUWMUVNUVOUVCUWMUVNTZTZUVOQRZUVJUGZUWPUVLUGZUEZQEUJZUWOU
      WSQEUWOUWPEUMZUWSUWOUXATZUWQYSUGZUWRYSUGZUEZUWSUXBUWPUVAUGZUVKUGZUXFUVMUG
      ZUXCUXDUWNUXGUXHUEZUVCUXAUVNUXIUWMUXFUVKUVMVHVIVJUVCUWMUVNUXAUXGUXCUEZUVC
      UWKUVNUXAUXJUIUIUWLUVCUWKUVNUXAUXJUVCUWKTUXAUXJUVNABOQCDEFGHIJKLMNVKVLVMV
      NVOUVCUWMUVNUXAUXHUXDUEZUVCUWLUVNUXAUXKUIUIUWKUVCUWLUVNUXAUXKUVCUWLTUXAUX
      KUVNABPQCDEFGHIJKLMNVKVLVMVPVOVQUVCUWNUXAUXEUWSVRZYTUWNUXATZUXLUVBCDYSUCU
      WQCUMZUWRCUMZTZUXLYTUXMCDUWQUWRYSVSCDYSVTUWMUXAUXPUVNUWMUXATUXNUXOUWKUXAU
      XNUWLECUWPUVJWAWBUWLUXAUXOUWKECUWPUVLWAWCWDWBUSWBWEWFWGVEUWMUVOUWTVRZUVCU
      VNUVJEWHZUVLEWHZUXQUWKUWLUXRUXSTUVOEEUEZUWTTUWTQEEUVJUVLWIUXTUWTEWJWKWLEC
      UVJWMECUVLWMUSWNWOWPUWIUWKUWJUWLCEUVJJLVCCEUVLJLVCWQWRWSWTVEWDOPUVDUVEIXA
      XBUVCUVGUVEXCZUVEUVGXCZTUVHUVCUYAUYBUVCUVIUYAUWHUVDUVEIXDURUVCOUVEUVGUVCU
      VJUVEUMZUWJUVMUVJUEZTZPXEZUVJUVGUMZUVCUYCYSULZUVJUKZUVAUKZUVDUMZUYJIUGZUV
      JUEZTZUYFUVCFDUVJUFZUYNUYCUVCUYOUYNUVCUYOTZUYKUYMUYPECUYJUFZUYKYTUYOUVBUY
      QFCUYIUFZEFUVAUFUYQYTUYOTZUVBEFCUYIUVAUNDCUYHUFZUYOUYRYTFDCUYHUVJUNYTDCUY
      HSUYTCDYSUQDCUYHUOURUPEFUVAUOUSXFCEUYJJLVCXBZUYPUYLYSUYJUKZUWAUKZUVJUYPUY
      KUYLVUCUEVUAABUYJUWBVUCUVDIUVSUYJUEUVTVUBUWAUVSUYJYSXGXHNVUBUWAYSUYJGXIZU
      YIUVAUYHUVJYSVUDXJOXIXKHXIZXKZXKUVAVUEXJXKXLURUYPYSUYHUKZUVJUKZUVAUWAUKZU
      KZUVJVUCUYPVUJUVJVUIUKZUVJYTUYOVUJVUKUEUVBUYSVUHUVJVUIYTUYOVUHXMDXNZUVJUK
      UVJYTVUGVULUVJCDYSXOXHFDUVJXPXQXHWBUVBUYOVUKUVJUEYTUVBUYOVUKUVJXMFXNZUKUV
      JUVBVUIVUMUVJEFUVAXOXRFDUVJXSXQWCXTVUCYSUYJUWAUKZUKYSUYIVUIUKZUKZVUJYSUYJ
      UWAYAVUNVUOYSUYIUVAUWAYAYBVUJYSUYIUKZVUIUKVUPVUHVUQVUIYSUYHUVJYAYCYSUYIVU
      IYAYDYEYFXTWDWGDFUVJKMVCWRUYEUYNPUYJVUFUVLUYJUEZUWJUYKUYDUYMUVLUYJUVDYGVU
      RUVMUYLUVJUVLUYJIYHYIYJYKYLUVCUVIIUVDWHUYGUYFVRUWHUVDUVEIWMPUVDUVJIYMYNYO
      YPWDUVGUVEYQXBWDUVDUVEIYRXB $.
      $([?]$)
  $}

  ${
    $d f g x y A $.  $d f g x y B $.  $d f g x y C $.  $d f g x y D $.
    mapen.1 $e |- A e. V $.
    mapen.2 $e |- B e. V $.
    mapen.3 $e |- C e. V $.
    mapen.4 $e |- D e. V $.
    $( Two mappings are equinumerous when their bases and exponents are
       equinumerous.  Theorem 6H(c) of [Enderton] p. 139. $)
    mapen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A ^m C ) ~~ ( B ^m D ) ) $=
      ( vf vg vx vy cv wf1o wex cm co cen wbr wi wa wcel ccom ccnv wceq copab
      cleqid mapenlem oprex f1oen syl exp 19.23aiv 19.23adv imp bren syl2anb )
      ABIMZNZIOZCDJMZNZJOZACPQZBDPQZRSZABRSCDRSUTVCVFUTVBVFJUSVBVFTIUSVBVFUSVBU
      AVDVEKMZVDUBLMURVGUCVAUDUCUEUAKLUFZNVFKLABCDIJVHEFGHVHUGUHVDVEVHACPUIUJUK
      ULUMUNUOABIFUPCDJHUPUQ $.
      $([?]$)
  $}

  ${
    $d x y z w v f g A $.  $d x y z w v f g B $.  $d x y z w v f g C $.
    mapxpen.1 $e |- A e. V $.
    mapxpen.2 $e |- B e. V $.
    mapxpen.3 $e |- C e. V $.
    $( Equinumerosity law for set exponentiation.  Proposition 10.45 of
       [TakeutiZaring] p. 96. $)
    mapxpen $p |- ( ( A ^m B ) ^m C ) ~~ ( A ^m ( B X. C ) ) $=
      ( vx vy vz vw vv vf vg cm co cxp cv wcel wa cfv wceq copab2 cop copab
      oprex cvv cleqid oprabex a1i wmo moeq funopabex pm5.32ri ancom 3bitr en2
      ) GHABNOZCNOZABCPNOZIQZBRZJQZCRZSKQUTVBGQZTTZUASIJKUBZVCLQVAMQUTVBUCHQZTU
      ASIMUDZUAZSJLUDZUQCNUEVFUFRVDURRIJKBCVEVFEFVFUGUHUIVJUFRVGUSRVIJLCFVILUJV
      CLVHUKUIULUI????????UMZ??UNVKUOUP $.
      $([?]$)
  $}

  ${
    pwen.1 $e |- A e. V $.
    pwen.2 $e |- B e. V $.
    $( If two sets are equinumerous, then their power sets are equinumerous.
       Proposition 10.15 of [TakeutiZaring] p. 87. $)
    pwen $p |- ( A ~~ B -> P~ A ~~ P~ B ) $=
      ( cen wbr c2o cm co cpw con0 wcel 2o enrefg ax-mp elisseti mapen mpan
      cvv wb oprex pw2en enen1 mp2an enen2 bitr2 sylib ) ABEFZGAHIZGBHIZEFZAJZB
      JZEFZGGEFZUHUKGKLUOMGKNOGGABGKMPZUPCDQRUNUIUMEFZUKUISLULUIEFUNUQTGAHUAACU
      BULUIUMSUCUDUJSLUMUJEFUQUKTGBHUABDUBUMUJUISUEUDUFUG $.
      $([?]$)
  $}

  ${
    $d x y A $.
    limenpsi.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to a proper subset of itself. $)
    limenpsi $p |- ( A e. B -> A ~~ ( A \ { (/) } ) ) $=
      ( vx vy c0 csn cdif cdom wbr cen wcel sbth cv csuc wn wa wlim wb limsuc
      ax-mp biimp wceq nsuceq0 visset sucex elsnc mtbir jctir eldif sylibr
      con0 weq suc11 word limord ordelon mpan syl2an dom2 wss difss ssdom2g
      mpi sylan13 ) AAFGZHZIJVGAIJZAVGKJABLZAVGMDEAVGDNZOZENZOZBVJALZVKALZVKVFL
      ZPZQVKVGLVNVOVQVNVOARZVNVOSCAVJTUAUBVPVKFUCVJUDVKFVJDUEUFUGUHUIVKAVFUJUKV
      JULLZVLULLZVKVMUCDEUMSVNVLALZVJVLUNAUOZVNVSVRWBCAUPUAZAVJUQURWBWAVTWCAVLU
      QURUSUTVIVGAVAVHAVFVBVGABVCVDVE $.
      $([31-Oct-03]$)
  $}

  ${
    limensuci.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to its successor. $)
    limensuci $p |- ( A e. B -> A ~~ suc A ) $=
      ( wcel c0 csn cdif cun csuc cen wbr wa cin wceq incom indif0 eqtr wn
      word wlim limord ax-mp ordeirr disjsn mpbir pm3.2i unen mpan2 cvv ensymg
      difexg limenpsi syl13 zfnull en2sn mpan sylan13 wss 0ellim snss mpbi
      undif3 uncom df-suc 3brtr4g ) ABDZAEFZGZVGHZAAFZHZAAIJVHAJKZVGVJJKZVIVKJK
      ZVFVLVMLVHVGMZENZAVJMENZLVNVPVQVOVGVHMEVHVGOVGAPQVQAADRZASZVRATZVSCAUAUBA
      UCUBAAUDUEUFVHAVGVJUGUHVHUIDAVHJKVLVFAVHUIUJAVGBUKABCULUMEUIDVFVMUNEAUIBU
      OUPUQAVGVHHZVIVGAURZAWANEADZWBVTWCCAUSUBEAUNUTVAVGAVBVAVGVHVCQAVDVE $.
      $([12-Nov-03]$)
  $}

  $( A limit ordinal is equinumerous to its successor. $)
  limensuc $p |- ( ( A e. B /\ Lim A ) -> A ~~ suc A ) $=
    ( wcel wlim csuc cen wbr wi con0 cded wceq eleq1 id suceq breq12d bi2imd
    limeq limon elimhyp limensuci dedth comm imp ) ABCZADZAAEZFGZUEUDUGUEUDUGHU
    EAIJZBCZUHUHEZFGZHAIAUHKZUDUIUGUKAUHBLULAUHUFUJFULMAUHNOPUHBUEUHDIDAIAUHQIU
    HQRSTUAUBUC $.
    $([12-Nov-03]$)

