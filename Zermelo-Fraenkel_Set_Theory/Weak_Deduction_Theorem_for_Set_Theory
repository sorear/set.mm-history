$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           "Weak Deduction Theorem" for Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( These lemmas are used to convert hypotheses into antecedents,
     when there is at least one class making the hypothesis true. $)

$(
The Weak Deduction Theorem in set theory
----------------------------------------

In a Hilbert system of logic (which consists of a set of axioms, modus
ponens, and the generalization rule), converting a deduction to a proof using
the Deduction Theorem (taught in introductory logic books) involves an
exponential increase of the number of steps as hypotheses are successively
eliminated.  Here is a trick that is not as general as the Deduction Theorem
but requires only a linear increase in the number of steps.

The general problem:  We want to convert a deduction
  P |- Q
into a proof of the theorem
  |- P -> Q
i.e. we want to eliminate the hypothesis P.  Normally this is done using the
Deduction (meta)Theorem, which looks at the microscopic steps of the
deduction and usually doubles or triples the number of these microscopic
steps for each hypothesis that is eliminated.  We will look at a special case
of this problem, without appealing to the Deduction Theorem.

We assume ZF with class notation.  A and B are arbitrary (possibly
proper) classes.  P, Q, R, S and T are wffs.

We define a "deduction class" ded(P,A,B) as follows:
  ded(P,A,B) =def= { x | (x \in A & P) v (x \in B & ~P) }
(where x does not occur in A, B, or P).

Lemma 1.
  A = ded(P,A,B) -> (P <-> R), B = ded(P,A,B) -> (S <-> R), S |- R
Proof:  Logic and Axiom of Extensionality.

Lemma 2.
  A = ded(P,A,B) -> (Q <-> T), T |- P -> Q
Proof:  Logic and Axiom of Extensionality.

Here's a simple example that illustrates how it works.  Suppose we have
a deduction
  Ord A |- Tr A
which means, "Assume A is an ordinal class. Then A is a transitive class."
Note that A is a class variable that may be substituted with any class
expression, so this is really a deduction scheme.

We want to convert this to a proof of the theorem (scheme)
  |- Ord A -> Tr A.

The catch is that we must be able to prove "Ord A" for at least one
object A (and this is what makes it weaker than the ordinary Deduction
Theorem).  However it is easy to prove |- Ord 0 (the empty set is
ordinal).  (For a typical textbook "theorem", i.e. deduction, there is
usually at least one object satisfying each hypothesis, otherwise the
theorem would not be very useful.  We can always go back to the standard
Deduction Theorem for those hypotheses where this is not the case.)
Continuing with the example:

Equality axioms (and Extensionality) yield
  |- A = ded( Ord A, A, 0) -> (Ord A <-> Ord ded( Ord A, A, 0))  (1)
  |- 0 = ded( Ord A, A, 0) -> (Ord 0 <-> Ord ded( Ord A, A, 0))  (2)
From (1), (2) and |- Ord 0, Lemma 1 yields
  |- Ord ded( Ord A, A, 0)                                       (3)
From (3) and substituting ded( Ord A, A, 0) for
A in the original deduction,
  |- Tr ded( Ord A, A, 0)                                        (4)
Equality axioms (and Extensionality) yield
  |- A = ded( Ord A, A, 0) -> (Tr A <-> Tr ded( Ord A, A, 0))    (5)
From (4) and (5), Lemma 2 yields
  |- Ord A -> Tr A                                               (Q.E.D.)
$)


  $( Declare new constant symbol. $)
  $c ded $.  $( Deduction class. $)
  $c , $.  $( Comma (also used for unordered pair notation later) $)

  $( Extend class notation to include deduction class.  See the Deduction
     Theorem link on the Metamath Proof Explorer Home Page for a description
     of the deduction class. $)
  cded $a class ded ( ph , A , B ) $.

  ${
    $d x ph $.  $d x A $.  $d x B $.
    $( Define the deduction class.  The purpose of the weak deduction
       (meta)theorem is to eliminate a hypothesis ` ph ` in a deduction by
       turning into the antecedent of the corresponding theorem form of the
       deduction.  ` A ` is a class variable in the hypothesis, and ` B ` is a
       class (usually a constant) that makes the hypothesis true when it is
       substituted for ` A ` . See ~ dedth for the main part of the weak
       deduction theorem, ~ elimhyp to eliminate a hypothesis, and ~ keephyp to
       keep a hypothesis.  See the Deduction Theorem link on the Metamath Proof
       Explorer Home Page for a description of the deduction class.  $)
    df-ded $a |- ded ( ph , A , B ) =
                 { x | ( ( x e. A /\ ph ) \/ ( x e. B /\ -. ph ) ) } $.
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for deduction classes. $)
    dedeq1 $p |- ( A = B -> ded ( ph , A , C ) = ded ( ph , B , C ) ) $=
      ( vx wceq cv wcel wa wn wo cab cded eleq2 birand birord biabdv df-ded
      3eqtr4g ) BCFZEGZBHZAIZUADHAJIZKZELUACHZAIZUDKZELABDMACDMTUEUHETUCUGUDTUB
      UFABCUANOPQAEBDRAECDRS $.
      $( [?] $) $( [1-Sep-04] $)

    $( Equality theorem for deduction classes. $)
    dedeq2 $p |- ( B = C -> ded ( ph , A , B ) = ded ( ph , A , C ) ) $=
      ( vx wceq cv wcel wa wn wo cab cded eleq2 birand bilord biabdv df-ded
      3eqtr4g ) CDFZEGZBHAIZUACHZAJZIZKZELUBUADHZUDIZKZELABCMABDMTUFUIETUEUHUBT
      UCUGUDCDUANOPQAEBCRAEBDRS $.
      $( [?] $) $( [1-Sep-04] $)

    $( Lemma for weak deduction theorem. $)
    dedlem1 $p |- ( ph -> ded ( ph , A , B ) = A ) $=
      ( vx cv wcel wa wn wo cab cded dedlema biabrdv df-ded syl6reqr ) ABDEZBFZ
      AGPCFZAHGIZDJABCKASDBAQRLMADBCNO $.
      $( [2-Sep-04] $) $( [15-May-99] $)

    $( Lemma for weak deduction theorem. $)
    dedlem2 $p |- ( -. ph -> ded ( ph , A , B ) = B ) $=
      ( vx wn cv wcel wa wo cab cded dedlemb biabrdv df-ded syl6reqr ) AEZCDFZB
      GZAHQCGZPHIZDJABCKPTDCARSLMADBCNO $.
      $( [2-Sep-04] $) $( [14-Aug-99] $)
  $}


  $( Equality theorem for deduction classes. $)
  dedeq12 $p |- ( ( A = B /\ C = D ) ->
                ded ( ph , A , C ) = ded ( ph , B , D ) ) $=
    ( wceq cded dedeq1 dedeq2 sylan9eq ) BCFDEFABDGACDGACEGABCDHACDEIJ $.
    $( [?] $) $( [1-Sep-04] $)

  $( Equivalence theorem for deduction classes.  (Contributed by Raph
     Levien, 15-Jan-04.) $)
  dedbi $p |- ( ( ph <-> ps ) -> ded ( ph , A , B ) = ded ( ps , A , B ) ) $=
    ( wb wa wn wo cded wceq dfbi dedlem1 cleqcomd sylan9eq dedlem2 jaoi sylbi
    ) ABEABFZAGZBGZFZHACDIZBCDIZJZABKRUDUAABUBCUCACDLBUCCBCDLMNSTUBDUCACDOTUCDB
    CDOMNPQ $.
    $( [18-Jan-04] $) $( [18-Jan-04] $)

  ${
    $d x A $.  $d x B $.  $d x ph $.  $( ` x ` is dummy. $)
    dedth.1 $e |- ( A = ded ( ph , A , B ) -> ( ps <-> ch ) ) $.
    dedth.2 $e |- ch $.
    $( Weak deduction theorem that eliminates a hypothesis ` ph ` , making it
       become an antecedent.  We assume that a proof exists for ` ph ` when
       the class variable ` A ` is replaced with a specific class ` B ` .
       The hypothesis ` ch ` should be assigned to the inference, and the
       inference's hypothesis eliminated with ~ elimhyp .  If the inference
       has other hypotheses with class variable ` A ` , these can be
       kept by assigning ~ keephyp to them.  For more information, see the
       Deduction Theorem link on the Metamath Proof Explorer home page. $)
    dedth $p |- ( ph -> ps ) $=
      ( cded wceq wb dedlem1 cleqcomd syl mpbiri ) ABCGADADEHZIBCJAODADEKLFMN
      $.
      $( [15-May-99] $)
  $}

  ${
    dedth2v.1 $e |- ( A = ded ( ph , A , C ) -> ( ps <-> ch ) ) $.
    dedth2v.2 $e |- ( B = ded ( ph , B , D ) -> ( ch <-> th ) ) $.
    dedth2v.3 $e |- th $.
    $( Weak deduction theorem for eliminating hypotheses with 2 class
       variables. $)
    dedth2v $p |- ( ph -> ps ) $=
      ( cded wceq wb dedlem1 cleqcomd syl bitrd mpbiri ) ABDKABCDAEAEGLZMBCNATE
      AEGOPIQAFAFHLZMCDNAUAFAFHOPJQRS $.
      $( [13-Aug-99] $)
  $}

  ${
    dedth3v.1 $e |- ( A = ded ( ph , A , D ) -> ( ps <-> ch ) ) $.
    dedth3v.2 $e |- ( B = ded ( ph , B , R ) -> ( ch <-> th ) ) $.
    dedth3v.3 $e |- ( C = ded ( ph , C , S ) -> ( th <-> ta ) ) $.
    dedth3v.4 $e |- ta $.
    $( Weak deduction theorem for eliminating hypothesis with 3 class
       variables. $)
    dedth3v $p |- ( ph -> ps ) $=
      ( cded wceq wb dedlem1 cleqcomd syl 3bitrd mpbiri ) ABEOABCDEAFAFIPZQBCRA
      UDFAFISTLUAAGAGJPZQCDRAUEGAGJSTMUAAHAHKPZQDERAUFHAHKSTNUAUBUC $.
      $( [13-Aug-99] $)
  $}

  ${
    dedth2h.1 $e |- ( A = ded ( ph , A , C ) -> ( ch <-> th ) ) $.
    dedth2h.2 $e |- ( B = ded ( ps , B , D ) -> ( th <-> ta ) ) $.
    dedth2h.3 $e |- ta $.
    $( Weak deduction theorem eliminating two hypotheses. $)
    dedth2h $p |- ( ( ph /\ ps ) -> ch ) $=
      ( wi cded wceq bilimd dedth imp ) ABCABCMBDMFHFAFHNOCDBJPBDEGIKLQQR $.
      $( [15-May-99] $)
  $}

  ${
    $v et $. $( Greek eta $)
    $v ze $. $( Greek zeta $)
    dedth3h.we $f wff et $.
    dedth3h.wz $f wff ze $.
    dedth3h.1 $e |- ( A = ded ( ph , A , D ) -> ( th <-> ta ) ) $.
    dedth3h.2 $e |- ( B = ded ( ps , B , R ) -> ( ta <-> et ) ) $.
    dedth3h.3 $e |- ( C = ded ( ch , C , S ) -> ( et <-> ze ) ) $.
    dedth3h.4 $e |- ze $.
    $( Weak deduction theorem eliminating three hypotheses. $)
    dedth3h $p |- ( ( ph /\ ps /\ ch ) -> th ) $=
      ( wa wi cded wceq bilimd dedth2h dedth exp3a 3imp ) ABCDABCDABCRZDSUGESFI
      FAFITUADEUGNUBBCELMGHJKOPQUCUDUEUF $.
      $( [15-May-99] $)
  $}

  ${
    $v et $. $( Greek eta $)
    $v ze $. $( Greek zeta $)
    $v si $. $( Greek sigma $)
    $v rh $. $( Greek rho $)
    $v F $.
    $v G $.
    dedth4h.we $f wff et $.
    dedth4h.wz $f wff ze $.
    dedth4h.ws $f wff si $.
    dedth4h.wr $f wff rh $.
    dedth4h.F $f class F $.
    dedth4h.G $f class G $.
    dedth4h.1 $e |- ( A = ded ( ph , A , R ) -> ( ta <-> et ) ) $.
    dedth4h.2 $e |- ( B = ded ( ps , B , S ) -> ( et <-> ze ) ) $.
    dedth4h.3 $e |- ( C = ded ( ch , C , F ) -> ( ze <-> si ) ) $.
    dedth4h.4 $e |- ( D = ded ( th , D , G ) -> ( si <-> rh ) ) $.
    dedth4h.5 $e |- rh $.
    $( Weak deduction theorem eliminating four hypotheses. $)
    dedth4h $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) -> ta ) $=
      ( wa wi cded wceq bilimd dedth2h imp ) ABUCCDUCZEABUJEUDUJLUDUJMUDFGJKFAF
      JUEUFELUJRUGGBGKUEUFLMUJSUGCDMNOHIPQTUAUBUHUHUI $.
      $( [16-May-99] $)
  $}

  ${
    elimhyp.1 $e |- ( A = ded ( ph , A , B ) -> ( ph <-> ps ) ) $.
    elimhyp.2 $e |- ( B = ded ( ph , A , B ) -> ( ch <-> ps ) ) $.
    elimhyp.3 $e |- ch $.
    $( Eliminate a hypothesis containing class variable ` A ` when it is known
       for a specific class ` B ` .  For more information, see the Deduction
       Theorem link on the Metamath Proof Explorer home page. $)
    elimhyp $p |- ps $=
      ( cded wceq wb dedlem1 cleqcomd syl ibi wn dedlem2 mpbii pm2.61i ) ABABAD
      ADEIZJABKATDADELMFNOAPZCBHUAETJCBKUATEADEQMGNRS $.
      $( [15-May-99] $)
  $}

  ${
    $v et $. $( Greek eta $)
    elimhyp2v.we $f wff et $.
    elimhyp2v.1 $e |- ( A = ded ( ph , A , C ) -> ( ph <-> ch ) ) $.
    elimhyp2v.2 $e |- ( B = ded ( ph , B , D ) -> ( ch <-> th ) ) $.
    elimhyp2v.3 $e |- ( C = ded ( ph , A , C ) -> ( ta <-> et ) ) $.
    elimhyp2v.4 $e |- ( D = ded ( ph , B , D ) -> ( et <-> th ) ) $.
    elimhyp2v.5 $e |- ta $.
    $( Eliminate a hypothesis containing 2 class variables. $)
    elimhyp2v $p |- th $=
      ( cded wceq wb dedlem1 cleqcomd syl bitrd ibi wn dedlem2 mpbii pm2.61i )
      ACACAABCAEAEGOZPABQAUGEAEGRSJTAFAFHOZPBCQAUHFAFHRSKTUAUBAUCZDCNUIDICUIGUG
      PDIQUIUGGAEGUDSLTUIHUHPICQUIUHHAFHUDSMTUAUEUF $.
      $( [14-Aug-99] $)
  $}

  ${
    $v et $. $( Greek eta $)
    $v ze $. $( Greek zeta $)
    $v si $. $( Greek sigma $)
    elimhyp3v.we $f wff et $.
    elimhyp3v.wz $f wff ze $.
    elimhyp3v.ws $f wff si $.
    elimhyp3v.1 $e |- ( A = ded ( ph , A , D ) -> ( ph <-> ch ) ) $.
    elimhyp3v.2 $e |- ( B = ded ( ph , B , R ) -> ( ch <-> th ) ) $.
    elimhyp3v.3 $e |- ( C = ded ( ph , C , S ) -> ( th <-> ta ) ) $.
    elimhyp3v.4 $e |- ( D = ded ( ph , A , D ) -> ( et <-> ze ) ) $.
    elimhyp3v.5 $e |- ( R = ded ( ph , B , R ) -> ( ze <-> si ) ) $.
    elimhyp3v.6 $e |- ( S = ded ( ph , C , S ) -> ( si <-> ta ) ) $.
    elimhyp3v.7 $e |- et $.
    $( Eliminate a hypothesis containing 3 class variables. $)
    elimhyp3v $p |- ta $=
      ( cded wceq wb dedlem1 cleqcomd syl 3bitrd ibi wn dedlem2 mpbii pm2.61i
      ) ADADAABCDAEAEHUAZUBABUCAUMEAEHUDUENUFAFAFIUAZUBBCUCAUNFAFIUDUEOUFAGAGJU
      AZUBCDUCAUOGAGJUDUEPUFUGUHAUIZKDTUPKLMDUPHUMUBKLUCUPUMHAEHUJUEQUFUPIUNUBL
      MUCUPUNIAFIUJUERUFUPJUOUBMDUCUPUOJAGJUJUESUFUGUKUL $.
      $( [14-Aug-99] $)
  $}

  ${
    elimel.1 $e |- B e. C $.
    $( Eliminate a membership hypothesis for weak deduction theorem, when
       special case ` B e. C ` is provable. $)
    elimel $p |- ded ( A e. C , A , B ) e. C $=
      ( wcel cded eleq1 elimhyp ) ACEZIABFZCEBCEABAJCGBJCGDH $.
      $( [15-May-99] $)
  $}

  ${
    keephyp.1 $e |- ( A = ded ( ph , A , B ) -> ( ps <-> th ) ) $.
    keephyp.2 $e |- ( B = ded ( ph , A , B ) -> ( ch <-> th ) ) $.
    keephyp.3 $e |- ps $.
    keephyp.4 $e |- ch $.
    $( Transform a hypothesis ` ps ` that we want to keep (but contains the
       same class variable ` A ` used in the eliminated hypothesis) for use
       with the weak deduction theorem. $)
    keephyp $p |- th $=
      ( cded wceq wb dedlem1 cleqcomd syl mpbii wn dedlem2 pm2.61i ) ADABDIAEAE
      FKZLBDMAUAEAEFNOGPQARZCDJUBFUALCDMUBUAFAEFSOHPQT $.
      $( [15-May-99] $)
  $}

  ${
    $v et $. $( Greek eta $)
    $v ze $. $( Greek zeta $)
    $v si $. $( Greek sigma $)
    $v rh $. $( Greek rho $)
    keephyp3v.we $f wff et $.
    keephyp3v.wz $f wff ze $.
    keephyp3v.ws $f wff si $.
    keephyp3v.wr $f wff rh $.
    keephyp3v.1 $e |- ( A = ded ( ph , A , D ) -> ( rh <-> ch ) ) $.
    keephyp3v.2 $e |- ( B = ded ( ph , B , R ) -> ( ch <-> th ) ) $.
    keephyp3v.3 $e |- ( C = ded ( ph , C , S ) -> ( th <-> ta ) ) $.
    keephyp3v.4 $e |- ( D = ded ( ph , A , D ) -> ( et <-> ze ) ) $.
    keephyp3v.5 $e |- ( R = ded ( ph , B , R ) -> ( ze <-> si ) ) $.
    keephyp3v.6 $e |- ( S = ded ( ph , C , S ) -> ( si <-> ta ) ) $.
    keephyp3v.7 $e |- rh $.
    keephyp3v.8 $e |- et $.
    $( Keep a hypothesis containing 3 class variables. $)
    keephyp3v $p |- ta $=
      ( cded wceq wb dedlem1 cleqcomd syl 3bitrd mpbii wn dedlem2 pm2.61i ) ADA
      NDUAANBCDAEAEHUCZUDNBUEAUNEAEHUFUGOUHAFAFIUCZUDBCUEAUOFAFIUFUGPUHAGAGJUCZ
      UDCDUEAUPGAGJUFUGQUHUIUJAUKZKDUBUQKLMDUQHUNUDKLUEUQUNHAEHULUGRUHUQIUOUDLM
      UEUQUOIAFIULUGSUHUQJUPUDMDUEUQUPJAGJULUGTUHUIUJUM $.
      $( [27-Sep-99] $)
  $}

  ${
    keepel.1 $e |- A e. C $.
    keepel.2 $e |- B e. C $.
    $( Keep a membership hypothesis for weak deduction theorem, when
       special case ` B e. C ` is provable. $)
    keepel $p |- ded ( ph , A , B ) e. C $=
      ( wcel cded eleq1 keephyp ) ABDGCDGABCHZDGBCBKDICKDIEFJ $.
      $( [14-Aug-99] $)
  $}

  ${
    dedex.1 $e |- A e. V $.
    dedex.2 $e |- B e. V $.
    $( Deduction class existence. $)
    dedex $p |- ded ( ph , A , B ) e. V $=
      ( cvv keepel ) ABCFDEG $.
      $( [?] $) $( [2-Sep-04] $)
  $}


