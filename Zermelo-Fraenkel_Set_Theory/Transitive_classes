$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Transitive classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare a new symbol. $)
  $c Tr $. $( Transitive predicate (read:  'the following class is
              transitive') $)

  $( Extend wff notation to include transitive classes.   Notation from
     [TakeutiZaring] p. 35. $)
  wtr $a wff Tr A $.

  $( Define a transitive class.  Not to be confused with a transitive
     relation (see ~ cotr ).  Definition of [Enderton] p. 71 extended to
     arbitrary classes.  For alternate definitions, see ~ dftr2 (which is
     suggestive of the word "transitive"), ~ dftr3 , and ~ dftr4 .  The term
     "complete" is used instead of "transitive" in Definition 3 of [Suppes]
     p. 130. $)
  df-tr $a |- ( Tr A <-> U. A (_ A ) $.

  ${
    $d x y A $.
    $( An alternate way of defining a transitive class.  Exercise 7 of
       [TakeutiZaring] p. 40. $)
    dftr2 $p |- ( Tr A <-> A. x A. y ( ( x e. y /\ y e. A ) -> x e. A ) ) $=
      ( cuni wss cv wcel wi wal wtr wel wa dfss2 df-tr wex 19.23v eluni birim
      bitr4 bial 3bitr4 ) CDZCEAFZUBGZUCCGZHZAICJABKBFCGLZUEHBIZAIAUBCMCNUHUFAU
      HUGBOZUEHUFUGUEBPUDUIUEBUCCQRSTUA $.
      $([24-Apr-94]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( An alternate way of defining a transitive class.  Definition 7.1 of
       [TakeutiZaring] p. 35. $)
    dftr3 $p |- ( Tr A <-> A. x ( x e. A -> x (_ A ) ) $=
      ( vy wtr wel cv wcel wa wi wal wss dftr2 19.5 19.21v impexp bi2.04 bitr
      bial dfss2 bilim 3bitr4 3bitr ) BDCAEZAFZBGZHCFBGZIZAJCJUGCJZAJUEUDBKZIZA
      JCABLUGCAMUHUJAUEUCUFIZIZCJUEUKCJZIUHUJUEUKCNUGULCUGUCUEUFIIULUCUEUFOUCUE
      UFPQRUIUMUECUDBSTUARUB $.
      $([29-Aug-93]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( An alternate way of defining a transitive class.  Definition of
       [Enderton] p. 71. $)
    dftr4 $p |- ( Tr A <-> A (_ P~ A ) $=
      ( vx cv wcel cpw wi wal wss wtr visset elpw bilim bial dfss2 dftr3
      3bitr4r ) BCZADZQAEZDZFZBGRQAHZFZBGASHAIUAUCBTUBRQABJKLMBASNBAOP $.
      $([29-Aug-93]$)
  $}

  $( Equality theorem for the transitive class predicate. $)
  treq $p |- ( A = B -> ( Tr A <-> Tr B ) ) $=
    ( wceq cuni wss wtr unieq sseq1d sseq2 bitrd df-tr 3bitr4g ) ABCZADZAEZBDZB
    EZAFBFMOPAEQMNPAABGHABPIJAKBKL $.
    $([17-Sep-93]$)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( In a transitive class, the membership relation is transitive. $)
    trel $p |- ( Tr A -> ( ( B e. C /\ C e. A ) -> B e. A ) ) $=
      ( vx vy wtr wcel wi cv wceq eleq1 bilimd bi2imd wel eleq2 birimd wa wal
      dftr2 biimp 19.21bbi exp3a vtoclg com4l pm2.43 syl6 pm2.43b imp3a ) AFZBC
      GZCAGZBAGZUIUJUKULHZUIDIZCGZUKUNAGZHZHZHZUIUJUMHZHDBCUNBJZURUTUIVAUOUJUQU
      MUNBCKVAUPULUKUNBAKLMLUIUOUKUQHUQUKUIUOUKUPUIDENZEIZAGZUPHZHZHUSECAVCCJZV
      FURUIVGVBUOVEUQVCCUNOVGVDUKUPVCCAKPMLUIVBVDUPUIVBVDQUPHZDEUIVHERDRDEASTUA
      UBUCUDUKUPUEUFUCUGUH $.
      $([19-Apr-94]$)
  $}

  $( In a transitive class, the membership relation is transitive. $)
  trel3 $p |- ( Tr A -> ( ( B e. C /\ C e. D /\ D e. A ) -> B e. A ) ) $=
    ( wtr wcel w3a wa trel imland 3anass syl5ib syld ) AEZBCFZCDFZDAFZGZOCAFZHZ
    BAFNOPQHZHTRNUASOACDIJOPQKLABCIM $.
    $([19-Apr-94]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( An element of a transitive class is a subset of the class. $)
    trss $p |- ( Tr A -> ( B e. A -> B (_ A ) ) $=
      ( vx wtr wcel wss cv wi wceq eleq1 sseq1 bi2imd bilimd wal dftr3 biimp
      19.21bi vtoclg pm2.43b ) ADZBAEZBAFZTCGZAEZUCAFZHZHTUAUBHZHCBAUCBIZUFUGTU
      HUDUAUEUBUCBAJUCBAKLMTUFCTUFCNCAOPQRS $.
      $([7-Aug-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( The intersection of transitive classes is transitive. $)
    trin $p |- ( ( Tr A /\ Tr B ) -> Tr ( A i^i B ) ) $=
      ( vx wtr wa cv cin wcel wss wi wal trss im2anan9 elin ssin 3imtr4g
      19.21aiv dftr3 sylibr ) ADZBDZEZCFZABGZHZUCUDIZJZCKUDDUBUGCUBUCAHZUCBHZEU
      CAIZUCBIZEUEUFTUHUJUAUIUKAUCLBUCLMUCABNUCABOPQCUDRS $.
      $([9-May-94]$)
  $}

  $( The empty set is transitive. $)
  tr0 $p |- Tr (/) $=
    ( c0 wtr cpw wss nullss dftr4 mpbir ) ABAACZDHEAFG $.
    $([16-Sep-93]$)

  $( The universe is transitive. $)
  trv $p |- Tr V $=
    ( cvv wtr cuni wss ssv df-tr mpbir ) ABACZADHEAFG $.
    $([14-Sep-03]$)

