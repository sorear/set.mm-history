$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Replacement
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w ph $. $d x y z w $.
    $( Axiom of Replacement expressed more compactly, with fewest number of
       different variables. $)
    axrep $p |- E. x ( E. y A. z ( ph -> z = y ) ->
                        A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) $=
      ( vw weq wi wal wex wel wa wb cv visset eleq2 birand biexdv bilbid
      bialdv bilimd ax-4 syl4 19.20i 19.22i ax-rep syl ax-17 hbe1 hbbi hbal
      hba1 hban hbex birbid cbvex sylib vtocl 19.35ri ) ADCFZGZDHZCIZDBJZBCJZAC
      HZKZBIZLZDHZBVBBHZVCBEJZVEKZBIZLZDHZBIZGVJVIBIZGECMZCNECFZVPVQVJVSVOVIBVS
      VNVHDVSVMVGVCVSVLVFBVSVKVDVEEMVRBMZOPQRSQTVJDCJZVMLZDHZCIZVPVJVEUSGZDHZCI
      ZBHWDVBWGBVAWFCUTWEDVEAUSACUAUBUCUDUCAECDBUEUFWCVOCBWBBDWAVMBWABUGVLBUHUI
      UJVNCDVCVMCVCCUGVLCBVKVECVKCUGACUKULUMUIUJCBFZWBVNDWHWAVCVMVRVTDMOUNSUOUP
      UQUR $.
      $( [15-Aug-03] $)
  $}

  ${
    $d x y z w $.
    $( Axiom of Replacement slightly strengthened from ~ axrep ; ` w ` may
       occur free in ` ph ` . $)
    axrep2 $p |- E. x ( E. y A. z ( ph -> z = y ) ->
                        A. z ( z e. x <-> E. x ( x e. w /\ A. y ph ) ) ) $=
      ( weq wi wal wex wel wa wb cv hbe1 ax-17 hba1 hban hbex hbbi hbal hbim
      visset a14b birand biexdv bilbid bialdv bilimd axrep vtoclf ) ADCFGDHZCIZ
      DBJZBCJZACHZKZBIZLZDHZGZBIULUMBEJZUOKZBIZLZDHZGZBICEMVFCBULVECUKCNVDCDUMV
      CCUMCOVBCBVAUOCVACOACPQRSTUAREUBCEFZUTVFBVGUSVEULVGURVDDVGUQVCUMVGUPVBBVG
      UNVAUOCEBUCUDUEUFUGUHUEABCDUIUJ $.
      $( [2-Jan-97] $)
  $}

  ${
    $d x y z w $.
    zfrep2.1 $e |- ( ph -> A. z ph ) $.
    $( A more traditional version of the Axiom of Replacement. $)
    zfrep2 $p |- ( A. x E. z A. y ( ph -> y = z ) ->
                E. z A. y ( y e. z <-> E. x ( x e. w /\ ph ) ) ) $=
      ( weq wi wal wex wel wa wb axrep2 19.35i ax-17 hba1 hban hbex hbbi hbal
      hbe1 a14b ax-4 impbi bilan biex a1i bi2bid biald cbvex sylib ) ACDGHCIDJZ
      BICBKZBEKZADIZLZBJZMZCIZBJCDKZUOALZBJZMZCIZDJUMUTBABDCENOUTVEBDUSDCUNURDU
      NDPUQDBUOUPDUODPADQRSTUAVDBCVAVCBVABPVBBUBTUABDGZUSVDCVFCPVFUNVAURVCBDCUC
      URVCMVFUQVBBUPAUOUPAADUDFUEUFUGUHUIUJUKUL $.
      $( [14-Aug-94] $)
  $}

  ${
    $d x y z w $.
    zfrep3.1 $e |- ( ph -> A. z ph ) $.
    $( Axiom of Replacement (similar to Axiom Rep of [BellMachover] p. 463).
       The antecedent tells us ` ph ` is analogous to a "function" from ` x `
       to ` y ` (although it is not really a function since it is a wff and not
       a class).  In the consequent we postulate the existence of a set ` z `
       that corresponds to the "image" of ` ph ` restricted to some other set
       ` w ` . The hypothesis says ` z ` must not be free in ` ph ` . $)
    zfrep3 $p |- ( A. x ( x e. w -> E. z A. y ( ph -> y = z ) ) ->
                E. z A. y ( y e. z <-> E. x ( x e. w /\ ph ) ) ) $=
      ( wel weq wi wal wex wa wb 19.37v impexp bial 19.21v bitr2 biex bitr3
      ax-17 hban zfrep2 sylbi anabs5 bilbi sylib ) BEGZACDHZIZCJZDKIZBJZCDGZUHU
      HALZLZBKZMZCJZDKZUNUOBKZMZCJZDKUMUOUIIZCJZDKZBJUTULVFBULUHUKIZDKVFUHUKDNV
      GVEDVEUHUJIZCJVGVDVHCUHAUIOPUHUJCQRSTPUOBCDEUHADUHDUAFUBUCUDUSVCDURVBCUQV
      AUNUPUOBUHAUESUFPSUG $.
      $( [26-Nov-95] $)
  $}

  ${
    $d y z A v $. $d z ph v $. $d A w v $.  $d x y z v $.  $d x w v $.
      $( ` v ` is dummy. $)
    zfrepclf.1 $e |- ( w e. A -> A. x w e. A ) $.
    zfrepclf.2 $e |- A e. V $.
    zfrepclf.3 $e |- ( x e. A -> E. z A. y ( ph -> y = z ) ) $.
    $( An inference rule based on the Axiom of Replacement.
       Typically, ` ph ` defines a function from ` x ` to ` y ` . $)
    zfrepclf $p |- E. z A. y ( y e. z <-> E. x ( x e. A /\ ph ) ) $=
      ( vv wel cv wcel wa wex wb wal wceq weq wi ax-17 hbeq eleq2 syl6bi
      19.21ai zfrep3 syl birand biexd bilbid bialdv biexdv mpbid vtocle ) CDKZB
      LZFMZANZBOZPZCQZDOZJFHJLZFRZUOBJKZANZBOZPZCQZDOZVBVDVEACDSTCQDOZTZBQVJVDV
      LBBEEVCFEJKBUAGUBZVDVEUQVKVCFUPUCZIUDUEABCDJADUAUFUGVDVIVADVDVHUTCVDVGUSU
      OVDVFURBVMVDVEUQAVNUHUIUJUKULUMUN $.
      $( [26-Nov-95] $)
  $}

  ${
    $d x y z A $. $d z ph $.
    zfrep3cl.1 $e |- A e. V $.
    zfrep3cl.2 $e |- ( x e. A -> E. z A. y ( ph -> y = z ) ) $.
    $( An inference rule based on the Axiom of Replacement.
       Typically, ` ph ` defines a function from ` x ` to ` y ` . $)
    zfrep3cl $p |- E. z A. y ( y e. z <-> E. x ( x e. A /\ ph ) ) $=
      ( cv wcel ax-17 zfrepclf ) ABCDCECHEIBJFGK $.
      $( [26-Nov-95] $)
  $}

  ${
    $d ph y z $.  $d ps z $.  $d x y z $.
    zfrep4.1 $e |- { x | ph } e. V $.
    zfrep4.2 $e |- ( ph -> E. z A. y ( ps -> y = z ) ) $.
    $( A version of Replacement using abstraction classes. $)
    zfrep4 $p |- { y | E. x ( ph /\ ps ) } e. V $=
      ( cv cab wcel wa wex cvv abid biran biex biabi wceq wel wb wal hbab1 weq
      wi sylbi zfrepclf cleqab mpbir issetri eqeltrr ) CHACIZJZBKZCLZDIZABKZCLZ
      DIMUNUQDUMUPCULABACNZOPQEUOEHZUORZELDESUNTDUAZELBCDEDUKACDUBFULABDEUCUDDU
      AELURGUEUFUTVAEUNDUSUGPUHUIUJ $.
      $( [26-Nov-95] $)
  $}


