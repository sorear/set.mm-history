$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Replacement
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Axiom of Replacement slightly strengthened; ` w ` may occur free in
       ` ph `. $)
    axrep $p |- E. x ( E. y A. z ( ph -> z = y ) ->
                        A. z ( z e. x <-> E. x ( x e. w /\ A. y ph ) ) ) $=
      ( weq wi wal wex wel wa wb cv hbe1 ax-17 hba1 hban hbex hbbi hbal hbim
      visset a14b birand biexdv bilbid bialdv bilimd ax-rep vtoclb ) ADCFGDHZCI
      ZDBJZBCJZACHZKZBIZLZDHZGZBIULUMBEJZUOKZBIZLZDHZGZBICEMVFCBULVECUKCNVDCDUM
      VCCUMCOVBCBVAUOCVACOACPQRSTUAREUBCEFZUTVFBVGUSVEULVGURVDDVGUQVCUMVGUPVBBV
      GUNVAUOCEBUCUDUEUFUGUHUEABCDUIUJ $.
      $([2-Jan-97]$)
  $}

  ${
    $d x y z w $.
    zfrep2.1 $e |- ( ph -> A. z ph ) $.
    $( A more traditional version of the Axiom of Replacement. $)
    zfrep2   $p |- ( A. x E. z A. y ( ph -> y = z ) ->
                E. z A. y ( y e. z <-> E. x ( x e. w /\ ph ) ) ) $=
      ( weq wi wal wex wel wa wb axrep 19.35i ax-17 hba1 hban hbex hbbi hbal
      hbe1 ax17eq a14b ax-4 impbi bilan biex a1i bi2bid biald cbvex sylib ) ACD
      GHCIDJZBICBKZBEKZADIZLZBJZMZCIZBJCDKZUPALZBJZMZCIZDJUNVABABDCENOVAVFBDUTD
      CUOUSDUODPURDBUPUQDUPDPADQRSTUAVEBCVBVDBVBBPVCBUBTUABDGZUTVECBDCUCVGUOVBU
      SVDBDCUDUSVDMVGURVCBUQAUPUQAADUEFUFUGUHUIUJUKULUM $.
      $([14-Aug-94]$)
  $}

  ${
    $d x y z w $.
    zfrep3.1 $e |- ( ph -> A. z ph ) $.
    $( Axiom of Replacement (similar to Bell and Machover, p. 463). $)
    zfrep3   $p |- ( A. x ( x e. w -> E. z A. y ( ph -> y = z ) ) ->
                E. z A. y ( y e. z <-> E. x ( x e. w /\ ph ) ) ) $=
      ( wel wa weq wi wal wex wb ax-17 hban zfrep2 19.37v impexp bial 19.21v
      bitr2 biex bitr3 anabs5 bilbi 3imtr4 ) BEGZAHZCDIZJZCKZDLZBKCDGZUGUHHZBLZ
      MZCKZDLUGAUIJZCKZDLJZBKUMUHBLZMZCKZDLUHBCDEUGADUGDNFOPUTULBUTUGUSJZDLULUG
      USDQVDUKDUKUGURJZCKVDUJVECUGAUIRSUGURCTUAUBUCSVCUQDVBUPCVAUOUMUHUNBUGAUDU
      BUESUBUF $.
      $([26-Nov-95]$)
  $}

  ${
    $d y z A v $. $d z ph v $. $d A w v $.  $d x y z v $.  $d x w v $.
      $( ` v ` is dummy. $)
    zfrepclf.1 $e |- ( w e. A -> A. x w e. A ) $.
    zfrepclf.2 $e |- A e. V $.
    zfrepclf.3 $e |- ( x e. A -> E. z A. y ( ph -> y = z ) ) $.
    $( An inference rule based on the Axiom of Replacement.
       Typically, ` ph ` defines a function from ` x ` to ` y `. $)
    zfrepclf   $p |- E. z A. y ( y e. z <-> E. x ( x e. A /\ ph ) ) $=
      ( vv wel cv wcel wa wex wb wal wceq weq wi ax-17 hbeq eleq2 syl6bi
      19.21ai zfrep3 syl birand biexd bilbid bialdv biexdv mpbid vtoclei ) CDKZ
      BLZFMZANZBOZPZCQZDOZJFHJLZFRZUOBJKZANZBOZPZCQZDOZVBVDVEACDSTCQDOZTZBQVJVD
      VLBBEEVCFEJKBUAGUBZVDVEUQVKVCFUPUCZIUDUEABCDJADUAUFUGVDVIVADVDVHUTCVDVGUS
      UOVDVFURBVMVDVEUQAVNUHUIUJUKULUMUN $.
      $([26-Nov-95]$)
  $}

  ${
    $d x y z A $. $d z ph $.
    zfrep3cl.1 $e |- A e. V $.
    zfrep3cl.2 $e |- ( x e. A -> E. z A. y ( ph -> y = z ) ) $.
    $( An inference rule based on the Axiom of Replacement.
       Typically, ` ph ` defines a function from ` x ` to ` y `. $)
    zfrep3cl   $p |- E. z A. y ( y e. z <-> E. x ( x e. A /\ ph ) ) $=
      ( cv wcel ax-17 zfrepclf ) ABCDCECHEIBJFGK $.
      $([26-Nov-95]$)
  $}

  ${
    $d ph y z $.  $d ps z $.  $d x y z $.
    zfrep4.1 $e |- { x | ph } e. V $.
    zfrep4.2 $e |- ( ph -> E. z A. y ( ps -> y = z ) ) $.
    $( A version of Replacement using abstraction classes. $)
    zfrep4   $p |- { y | E. x ( ph /\ ps ) } e. V $=
      ( wa wex cab cv wcel cvv abid biran biex biab wceq wel wb wal hbab1 weq
      wi sylbir zfrepclf cleqab mpbir issetri eqeltr ) ABHZCIZDJCKACJZLZBHZCIZD
      JZMULUPDUKUOCAUNBACNZOPQEUQEKZUQRZEIDESUPTDUAZEIBCDEDUMACDUBFUNABDEUCUDDU
      AEIURGUEUFUTVAEUPDUSUGPUHUIUJ $.
      $([26-Nov-95]$)
  $}

