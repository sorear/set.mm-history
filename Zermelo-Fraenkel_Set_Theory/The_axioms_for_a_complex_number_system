$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms for a complex number system
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The class of complex numbers is a set. $)
  axcnex $p |- CC e. V $=
      ( cc cnr cxp cvv df-c nrex xpex eqeltr ) ABBCDEBBFFGH $.
      $([13-Aug-95]$)

  $( The real numbers are a subset of the complex numbers. $)
  axresscn $p |- RR (_ CC $=
      ( cnr c0r csn cxp cr cc wss ssxp ssid wcel snssi 0r ax-mp mp2an df-r
      df-c 3sstr4 ) ABCZDZAADZEFAAGRAGZSTGAARAHAIBAJUABAKLMNOPQ $.
      $([1-Mar-95]$)

  $( 0 is a real number.  Note that the subscript ` _10 `, meaning base 10,
     indicates the end of a digit string to eliminate ambiguity $)
  ax0re $p |- 0 _10 e. RR $=
      ( cn0 c0r cop cr df-0 wcel cnr opelreal 0r mpbir eqeltr ) ABBCZDELDFBGFBH
      IJK $.
      $([1-Mar-95]$)

  $( 1 is a real number. $)
  ax1re $p |- 1 _10 e. RR $=
      ( cn1 c1r c0r cop cr df-1 wcel cnr opelreal 1r mpbir eqeltr ) ABCDZEFMEGB
      HGBIJKL $.
      $([1-Mar-95]$)

  $( ` i ` is a complex number. $)
  axicn $p |- i e. CC $=
      ( cni cc wcel c0r cnr c1r cop wa df-i cleq3i 1r elisseti opelcn bitr
      mpbiranr 0r mpbir ) ABCZDECZRSFECZRDFGZBCSTHAUABIJDFFEKLMNKOPQ $.
      $([23-Feb-96]$)

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Closure law for addition of complex numbers. $)
    axaddcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) e. CC ) $=
      ( vx vy vz vw cc wcel wa caddc co cnr cxp cv cop df-c wceq opreq1 cleq3d
      opreq2 cplr addcnsr addclsr im2an an4s opelxpi syl mpbird 2optocl cleq4i
      sylibr ) AGHBGHIABJKZLLMZHZULGHCNZDNZOZENZFNZOZJKZUMHZAUTJKZUMHUNCDEFABLL
      GPUQAQVAVCUMUQAUTJRSUTBQVCULUMUTBAJTSUOLHZUPLHZIURLHZUSLHZIIZVBUOURUAKZUP
      USUAKZOZUMHZVHVAVKUMUOUPURUSUBSVHVILHZVJLHZIZVLVDVFVEVGVOVDVFIVMVEVGIVNUO
      URUCUPUSUCUDUEVIVJLLUFUGUHUIGUMULPUJUK $.
      $([14-Jun-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Closure law for addition of real subfield of complex numbers. $)
    axaddrcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A + B ) e. RR ) $=
      ( vx vy cv c0r cop caddc co cr wcel cnr elreal wceq opreq1 cleq3d
      opreq2 wa cplr addresr addclsr opelreal sylibr mpbird 2gencl ) CEZFGZDEZ
      FGZHIZJKZAUIHIZJKABHIZJKCDUGUIABLJCAMDBMUGANUJULJUGAUIHOPUIBNULUMJUIBAHQP
      UFLKUHLKRZUKUFUHSIZFGZJKZUNUJUPJUFUHTPUNUOLKUQUFUHUAUOUBUCUDUE $.
      $([31-Mar-96]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Closure law for multiplication of complex numbers. $)
    axmulcl $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) e. CC ) $=
      ( vx vy vz vw cc wcel wa cmulc co cnr cxp cv cop df-c wceq opreq1 cleq3d
      opreq2 cmr cm1r cplr mulcnsr addclsr mulclsr m1r mpan syl syl2an an4s
      ancoms an41r3s jca opelxpi mpbird 2optocl cleq4i sylibr ) AGHBGHIABJKZLLM
      ZHZUTGHCNZDNZOZENZFNZOZJKZVAHZAVHJKZVAHVBCDEFABLLGPVEAQVIVKVAVEAVHJRSVHBQ
      VKUTVAVHBAJTSVCLHZVDLHZIVFLHZVGLHZIIZVJVCVFUAKZUBVDVGUAKZUAKZUCKZVDVFUAKZ
      VCVGUAKZUCKZOZVAHZVPVIWDVAVCVDVFVGUDSVPVTLHZWCLHZIWEVPWFWGVLVNVMVOWFVQLHV
      SLHZWFVLVNIVMVOIZVQVSUEVCVFUFWIVRLHZWHVDVGUFUBLHWJWHUBVRUFUGUHUIUJUKVLVOV
      MVNWGVMVNIZVLVOIZWGWALHWBLHWGWKWLWAWBUEVDVFUFVCVGUFUJULUMUNVTWCLLUOUIUPUQ
      GVAUTPURUS $.
      $([10-Aug-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Closure law for multiplication of real subfield of complex numbers. $)
    axmulrcl $p |- ( ( A e. RR /\ B e. RR ) -> ( A x. B ) e. RR ) $=
      ( vx vy cv c0r cop cmulc co cr wcel cnr elreal wceq opreq1 cleq3d
      opreq2 wa cmr visset mulresr mulclsr opelreal sylibr mpbird 2gencl ) CEZ
      FGZDEZFGZHIZJKZAUJHIZJKABHIZJKCDUHUJABLJCAMDBMUHANUKUMJUHAUJHOPUJBNUMUNJU
      JBAHQPUGLKUILKRZULUGUISIZFGZJKZUOUKUQJUGUIDTUAPUOUPLKURUGUIUBUPUCUDUEUF
      $.
      $([31-Mar-96]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Addition of complex numbers is commutative. $)
    axaddcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A + B ) = ( B + A ) ) $=
      ( vx vy vz vw cc cv cplr co cep ccnv cnr caddc dfcnqs addcnsrec visset
      addcomsr ecoprcom ) CDEFABGCHZEHZIJKLMNDHZFHZIJUATIJUCUBIJOTUBUAUCPUAUCTU
      BPTUACQEQRUBUCDQFQRS $.
      $([31-Aug-95]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $( ` x y z w ` are dummy. $)
    $( Multiplication of complex numbers is commutative. $)
    axmulcom $p |- ( ( A e. CC /\ B e. CC ) -> ( A x. B ) = ( B x. A ) ) $=
      ( vx vy vz vw cc cv cmr co cm1r cplr cep ccnv cnr cmulc dfcnqs mulcnsrec
      visset mulcomsr opreq2i opreq12i oprex addcomsr eqtr ecoprcom ) CDEFABGCH
      ZEHZIJZKDHZFHZIJZIJZLJMNOPUJUHIJZUGUKIJZLJZUHUGIJZKUKUJIJZIJZLJUKUGIJZUHU
      JIJZLJZQUGUJUHUKRUHUKUGUJRUIUQUMUSLUGUHCSZESZTULURKIUJUKDSZFSZTUAUBUPVAUT
      LJVBUNVAUOUTLUJUHVEVDTUGUKVCVFTUBVAUTUHUJIUCUKUGIUCUDUEUF $.
      $([31-Aug-95]$)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
      $( ` x y z w v u ` are dummy. $)
    $( Addition of complex numbers is associative. $)
    axaddass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A + B ) + C ) = ( A + ( B + C ) ) ) $=
      ( vx vy vz vw vv vu cc cep ccnv cnr caddc cv cplr co dfcnqs addcnsrec
      wcel wa addclsr im2an an4s visset addasssr ecoprass ) DEFGHIABCJKLMNDOZFO
      ZPQZEOZGOZPQZUJHOZPQUMIOZPQUHUIUNPQZPQUKULUOPQZPQUPUQRUHUKUIULSUIULUNUOSU
      JUMUNUOSUHUKUPUQSUHMTZUIMTZUKMTZULMTZUJMTZUMMTZUAURUSUAVBUTVAUAVCUHUIUBUK
      ULUBUCUDUSUNMTZVAUOMTZUPMTZUQMTZUAUSVDUAVFVAVEUAVGUIUNUBULUOUBUCUDUHUIUNF
      UEHUEUFUKULUOGUEIUEUFUG $.
      $([2-Sep-95]$)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u f g h $.  $( ` x y z w v u f g h ` are dummy. $)
    $( Multiplication of complex numbers is associative. $)
    axmulass $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                 ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) ) $=
      ( vx vy vz vw vv vu vf vg vh cc cep ccnv cnr cmulc cv cmr co cm1r cplr
      dfcnqs mulcnsrec wcel wa addclsr mulclsr m1r mpan syl syl2an an4s ancoms
      an41r3s jca oprex visset addcomsr addasssr caopr41r3 distrsr opreq2i
      eqtr opreq12i elisseti mulcomsr mulasssr caoprdilem caopr12 3eqtr
      3eqtr4r ecoprass ) DEFGHIABCMNOPQDRZFRZSTZUAERZGRZSTZSTZUBTZVQVOSTZVNVRST
      ZUBTZWAHRZSTZUAWDIRZSTZSTZUBTZWDWESTZWAWGSTZUBTZVNVOWESTZUAVRWGSTZSTZUBTZ
      STZUAVQVRWESTZVOWGSTZUBTZSTZSTZUBTZVQWQSTZVNXASTZUBTZWQXAUCVNVQVOVRUDVOVR
      WEWGUDWAWDWEWGUDVNVQWQXAUDVNPUEZVQPUEZUFVOPUEZVRPUEZUFZUFWAPUEZWDPUEZXHXJ
      XIXKXMVPPUEVTPUEZXMXHXJUFXIXKUFZVPVTUGVNVOUHXPVSPUEZXOVQVRUHUAPUEZXQXOUAV
      SUHUIUJUKULUMXHXKXIXJXNXIXJUFZXHXKUFZXNWBPUEWCPUEXNXSXTWBWCUGVQVOUHVNVRUH
      ULUNUOUPXLWEPUEZWGPUEZUFUFWQPUEZXAPUEZXJYAXKYBYCWNPUEWPPUEZYCXJYAUFXKYBUF
      ZWNWPUGVOWEUHYFWOPUEZYEVRWGUHXRYGYEUAWOUHUIUJUKULUMXJYBXKYAYDXKYAUFZXJYBU
      FZYDWSPUEWTPUEYDYHYIWSWTUGVRWEUHVOWGUHULUNUOUPVNWNSTZVNWPSTZUBTZUAVQWSSTZ
      STZUAVQWTSTZSTZUBTZUBTYJYNUBTZYPYKUBTZUBTXDWJJKLYJYKYNYPUBVNWNSUQVNWPSUQU
      AYMSUQJRZKRZJURZKURZUSZYTUVALRZUVCLURZUTZUAYOSUQVAWRYLXCYQUBVNWNWPVOWESUQ
      ZUAWOSUQZVBXCUAYMYOUBTZSTYQXBUVJUASVQWSWTVRWESUQZVOWGSUQZVBVCUAYMYOVQWSSU
      QVQWTSUQZVBVDVEWFYRWIYSUBWFYJUAVSWESTZSTZUBTYRJKLVNUAVOVSUBSWEDURZUAPUIVF
      ZFURZYTUVAUVBUVCVGZYTUVAUVEUVCUVFVBZVQVRSUQZHURZYTUVAUVEUVCUVFVHZVIUVOYNY
      JUBUVNYMUASVQVRWEGURZUWBVHVCVCVDWIUAYOVNWOSTZUBTZSTYPUAUWESTZUBTYSWHUWFUA
      SJKLVQVNVOVRUBSWGEURZUVPUVRUVSUVTUWDIURZUWCVIVCUAYOUWEUVMVNWOSUQVBUWGYKYP
      UBJKLUAVNWOSUVQUVPVRWGSUQZUVSUWCVJVCVKVEVLVQWNSTZVQWPSTZUBTZVNWSSTZVNWTST
      ZUBTZUBTUWKUWNUBTZUWOUWLUBTZUBTXGWMJKLUWKUWLUWNUWOUBVQWNSUQVQWPSUQVNWSSUQ
      UVDUVGVNWTSUQVAXEUWMXFUWPUBVQWNWPUVHUVIVBVNWSWTUVKUVLVBVEWKUWQWLUWRUBJKLV
      QVNVOVRUBSWEUWHUVPUVRUVSUVTUWDUWBUWCVIWLUWOUAVSWGSTZSTZUBTUWRJKLVNUAVOVSU
      BSWGUVPUVQUVRUVSUVTUWAUWIUWCVIUWTUWLUWOUBUWTUAVQWOSTZSTUWLUWSUXAUASVQVRWG
      UWDUWIVHVCJKLUAVQWOSUVQUWHUWJUVSUWCVJVDVCVDVEVLVM $.
      $([3-Sep-95]$)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u f g h $.  $( ` x y z w v u f g h ` are dummy. $)
    $( Distributive law for complex numbers. $)
    axdistr $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) ) $=
      ( vx vy vz vw vv vu vf vg vh cc cep ccnv cnr caddc cmulc cv cplr co cmr
      cm1r dfcnqs addcnsrec mulcnsrec wcel wa addclsr im2an an4s mulclsr m1r
      mpan syl syl2an ancoms an41r3s jca visset distrsr opreq2i oprex eqtr
      opreq12i addcomsr addasssr caopr4 ecoprdist ) DEFGHIABCMNOPQRDSZFSZHSZTUA
      ZUBUAZUCESZGSZISZTUAZUBUAZUBUAZTUAZVOVMUBUAZVJVRUBUAZTUAZVJVKUBUAZUCVOVPU
      BUAZUBUAZTUAZVJVLUBUAZUCVOVQUBUAZUBUAZTUAZTUAZVOVKUBUAZVJVPUBUAZTUAZVOVLU
      BUAZVJVQUBUAZTUAZTUAZVMVRWHWPWLWSUDVKVPVLVQUEVJVOVMVRUFVJVOVKVPUFVJVOVLVQ
      UFWHWPWLWSUEVKPUGZVLPUGZVPPUGZVQPUGZVMPUGZVRPUGZUHXAXBUHXEXCXDUHXFVKVLUIV
      PVQUIUJUKVJPUGZVOPUGZUHZXAXCUHUHWHPUGZWPPUGZXGXAXHXCXJWEPUGWGPUGZXJXGXAUH
      XHXCUHZWEWGUIVJVKULXMWFPUGZXLVOVPULUCPUGZXNXLUCWFULUMUNUOUPUKXGXCXHXAXKXH
      XAUHZXGXCUHZXKWNPUGWOPUGXKXPXQWNWOUIVOVKULVJVPULUPUQURUSXIXBXDUHUHWLPUGZW
      SPUGZXGXBXHXDXRWIPUGWKPUGZXRXGXBUHXHXDUHZWIWKUIVJVLULYAWJPUGZXTVOVQULXOYB
      XTUCWJULUMUNUOUPUKXGXDXHXBXSXHXBUHZXGXDUHZXSWQPUGWRPUGXSYCYDWQWRUIVOVLULV
      JVQULUPUQURUSWAWEWITUAZWGWKTUAZTUAWMVNYEVTYFTVJVKVLFUTZHUTZVAVTUCWFWJTUAZ
      UBUAYFVSYIUCUBVOVPVQGUTZIUTZVAVBUCWFWJVOVPUBVCVOVQUBVCVAVDVEJKLWEWIWGWKTV
      JVKUBVCVJVLUBVCUCWFUBVCJSZKSZJUTKUTZVFZYLYMLSYNLUTVGZUCWJUBVCVHVDWDWNWQTU
      AZWOWRTUAZTUAWTWBYQWCYRTVOVKVLYGYHVAVJVPVQYJYKVAVEJKLWNWQWOWRTVOVKUBVCVOV
      LUBVCVJVPUBVCYOYPVJVQUBVCVHVDVI $.
      $([2-Sep-95]$)
  $}

  $( 1 and 0 are distinct. $)
  ax1ne0 $p |- 1 _10 =/= 0 _10 $=
      ( cn1 cn0 wne wceq wn df-ne c1r c0r cop df-1 df-0 cleq12i cnr 1r
      elisseti eqresr 1ne0sr mtbir mpbir ) ABCABDZEABFTGHIZHHIZDZAUABUBJKLUCGHD
      GHGMNOPQRRS $.
      $([19-Mar-96]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( 0 is an identity element for addition. $)
    ax0id $p |- ( A e. CC -> ( A + 0 _10 ) = A ) $=
      ( vx vy cv cop cn0 caddc co wceq cnr cc df-c opreq1 id cleq12d wcel wa
      c0r cplr addcnsr 0r pm3.2i mpan2 opeq12 0idsr syl2an eqtrd df-0 opreq2i
      syl5eq optocl ) BDZCDZEZFGHZUNIAFGHZAIBCAJJKLUNAIZUOUPUNAUNAFGMUQNOULJPZU
      MJPZQZUNRREZGHZUNUOUTVBULRSHZUMRSHZEZUNUTRJPZVFQVBVEIULUMRRTVFVFUAUAUBUCV
      CULIVDUMIVEUNIURUSVCVDULUMUDULUEUMUEUFUGFVAUNGUHUIUJUK $.
      $([31-Mar-96]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( 1 is an identity element for multiplication. $)
    ax1id $p |- ( A e. CC -> ( A x. 1 _10 ) = A ) $=
      ( vx vy cv cop cn1 cmulc co wceq cnr cc df-c opreq1 id cleq12d wcel wa
      c1r c0r cmr cm1r cplr mulcnsr 1r 0r pm3.2i mpan2 00sr opreq2d m1r ax-mp
      syl6eq 1idsr opreq1d 0idsr eqtrd sylan9req sylan9eq jca opeq12 syl
      df-1 opreq2i syl5eq optocl ) BDZCDZEZFGHZVHIAFGHZAIBCAJJKLVHAIZVIVJVHAVHA
      FGMVKNOVFJPZVGJPZQZVHRSEZGHZVHVIVNVPVFRTHZUAVGSTHZTHZUBHZVGRTHZVFSTHZUBHZ
      EZVHVNRJPZSJPZQVPWDIVFVGRSUCWEWFUDUEUFUGVNVTVFIZWCVGIZQWDVHIVNWGWHVMVLVTV
      QSUBHZVFVMVSSVQUBVMVSUASTHZSVMVRSUATVGUHUIUAJPWJSIUAUHUJUKULUIVLWIVFSUBHV
      FVLVQVFSUBVFUMUNVFUOUPUQVLVMWCWASUBHZVGVLWBSWAUBVFUHUIVMWKVGSUBHVGVMWAVGS
      UBVGUMUNVGUOUPURUSVTWCVFVGUTVAUPFVOVHGVBVCVDVE $.
      $([31-Mar-96]$)
  $}

  ${
    $d x y z w v A $.  $( ` y z w v ` are dummy. $)
    $( Existence of negative of complex number. $)
    axnegex $p |- ( A e. CC -> E. x e. CC ( A + x ) = 0 _10 ) $=
      ( vy vz vw vv cc wcel cv caddc co cn0 wceq wa wex wrex cop cnr df-c
      opreq1 cleq1d biland biexdv cplr c0r cxp opelxpi cleq4i sylibr adantl
      adantr addcnsr opeq12 df-0 syl6eqr sylan9eq jca opex cleq3 opreq2 bi2and
      cla4ev syl exp31 imp3a an4 syl5ib 19.23advv negexsr im2an eeanv mpd
      optocl df-rex ) BGHAIZGHZBVOJKZLMZNZAOZVRAGPVPCIZDIZQZVOJKZLMZNZAOZVTCDBR
      RGSWCBMZWFVSAWHWEVRVPWHWDVQLWCBVOJTUAUBUCWARHZWBRHZNZEIZRHZWAWLUDKZUEMZNZ
      FIZRHZWBWQUDKZUEMZNZNZFOEOZWGWKXBWGEFWKWMWRNZWOWTNZNWGXBWKXDXEWGWKXDXEWGW
      KXDNZXENZWLWQQZGHZWCXHJKZLMZNZWGXGXIXKXFXIXEXDXIWKXDXHRRUFZHXIWLWQRRUGGXM
      XHSUHUIUJUKXFXEXJWNWSQZLWAWBWLWQULXEXNUEUEQLWNWSUEUEUMUNUOUPUQWFXLAXHWLWQ
      URVOXHMZVPXIWEXKVOXHGUSXOWDXJLVOXHWCJUTUAVAVBVCVDVEWMWOWRWTVFVGVHWKWPEOZX
      AFOZNXCWIXPWJXQEWAVIFWBVIVJWPXAEFVKUIVLVMVRAGVNUI $.
      $([3-May-96]$)
  $}

  ${
    $d x y z A $.  $d x y z w v u $.  $( ` y z w v u ` are dummy. $)
    $( Existence of reciprocal of nonzero complex number. $)
    axrecex $p |- ( A e. CC ->
                   ( A =/= 0 _10 -> E. x e. CC ( A x. x ) = 1 _10 ) ) $=
      ( vy vz vw vv vu cv cop cn0 wne cmulc co cn1 wceq cc wrex wi cnr df-c
      neeq1 opreq1 cleq1d birexdv bi2imd wcel wa wex cmr cplr c1r c0r wn cltr
      wbr visset ssgt0sr oprex recexsrlem syl6 df-ne df-0 cleq2i 0r elisseti
      opth bitr bineg syl5ib cm1r mulclsr m1r jctil syl im2an anandir opelcn
      3imtr4 adantarr pm3.26 sylbi jca mulcnsr cleq2 mulasssr cleqcomi a1i
      1idsr 3syl m1m1sr opreq1i mulcomsr caopr12 opreq2i eqtr4 1r 3eqtr4
      3eqtr4g opreq12d distrsr caoprdistrr syl6eqr syl5bi exp3a com3l imp32
      adantall anass sylan5 pn0sr eqtr opreq12i syl5eq opeq12 eqtrd df-1 opex
      cleq3 opreq2 bi2and cla4ev exp 19.23adv syld df-rex syl6ibr optocl ) CHZD
      HZIZJKZYJAHZLMZNOZAPQZRBJKZBYLLMZNOZAPQZRCDBSSPTYJBOZYKYPYOYSYJBJUAYTYNYR
      APYTYMYQNYJBYLLUBUCUDUEYHSUFZYISUFZUGZYKYLPUFZYNUGZAUHZYOUVCYKEHZSUFZYHYH
      UIMZYIYIUIMZUJMZUVGUIMZUKOZUGZEUHZUVFUVCYHULOYIULOUGZUMZUVOYKUVCUVQULUVKU
      NUOUVOYHYICUPZDUPZUQEUVKUVIUVJUJURUSUTYKYJJOZUMUVQYJJVAUVTUVPUVTYJULULIZO
      UVPJUWAYJVBVCYHYIULULUVRUVSULSVDVEVFVGVHVGVIUVCUVNUVFEUVCUVNUVFUVCUVNUGZY
      HUVGUIMZVJYIUVGUIMZUIMZIZPUFZYJUWFLMZNOZUGZUVFUWBUWGUWIUVCUVHUWGUVMUVAUVH
      UGZUVBUVHUGZUGZUWCSUFZUWESUFZUGZUVCUVHUGZUWGUWKUWNUWLUWOYHUVGVKUWLVJSUFZU
      WDSUFZUGUWOUWLUWSUWRYIUVGVKZVLVMVJUWDVKVNVOZUVAUVBUVHVPZUWCUWEVJUWDUIURVQ
      VRVSUWBUWHUKULIZNUWBUWHYHUWCUIMZVJYIUWEUIMZUIMZUJMZYIUWCUIMZYHUWEUIMZUJMZ
      IZUXCUWBUVCUWPUGZUWHUXKOUVCUVHUXLUVMUWQUVCUWPUVCUVHVTUWQUWMUWPUXBUXAWAWBV
      SYHYIUWCUWEWCVNUWBUXGUKOZUXJULOZUGUXKUXCOUWBUXMUXNUVBUVNUXMUVAUVBUVHUVMUX
      MUVMUVBUVHUXMUVMUVBUVHUXMUVMUXGUVLOUXMUWLUVLUKUXGWDUWLUXGUVIUVGUIMZUVJUVG
      UIMZUJMUVLUWLUXDUXOUXFUXPUJUXDUXOOUWLUXOUXDYHYHUVGUVREUPZWEWFWGUWLYIUWDUI
      MZUKUIMZUXRUXFUXPUWLUVBUWSUGUXRSUFUXSUXROUWLUVBUWSUVBUVHVTUWTWBYIUWDVKUXR
      WHWIVJVJUIMZUXRUIMZUKUXRUIMUXFUXSUXTUKUXRUIWJWKUXFVJVJUXRUIMZUIMUYAUXEUYB
      VJUIFGAYIVJUWDUIUVSVJSVLVEZYIUVGUIURZFHZGHZFUPGUPZWLZUYEUYFYLUYGAUPZWEZWM
      WNVJVJUXRUYCYIUWDUIURZWEWOUXRUKUYKUKSWPVEWLWQYIYIUVGUVSUXQWEWRWSFGAUVIUVJ
      UVGUJUIYHYHUIURYIYIUIURUXQUYHUYEUYFYLUYGUYIWTXAXBXCXDXEXFXGUWBYHUWDUIMZSU
      FZUXNUVCUVHUYMUVMUWQUVAUWLUGUYMUVAUVBUVHXHUVAUWSUYMUWLYHUWDVKUWTXIWAVSUYM
      UYLUYLVJUIMZUJMULUXJUYLXJUXHUYLUXIUYNUJFGAYIYHUVGUIUVSUVRUXQUYHUYJWMUXIVJ
      UYLUIMUYNFGAYHVJUWDUIUVRUYCUYDUYHUYJWMVJUYLUYCYHUWDUIURWLXKXLXMVNWBUXGUXJ
      UKULXNVNXOXPXBWBUVEUWJAUWFUWCUWEXQYLUWFOZUVDUWGYNUWIYLUWFPXRUYOYMUWHNYLUW
      FYJLXSUCXTYAVNYBYCYDYNAPYEYFYG $.
      $([14-May-96]$)
  $}

  ${
    $d x y A $.  $d x y z $.  $( ` y z ` are dummy. $)
    $( Existence of negative of real number. $)
    axrnegex $p |- ( A e. RR -> E. x e. RR ( A + x ) = 0 _10 ) $=
      ( vy vz cv c0r cop caddc co cn0 wceq cr wrex cnr wcel elreal opreq1
      cleq1d birexdv cplr wa wex wb addresr df-0 cleq2i oprex eqresr bitr
      syl6bb exp pm5.32d opex cleq3 opreq2 bi2and cla4ev opelreal sylanbr
      df-rex sylibr syl6bir 19.23adv negexsr mpd gencl ) CEZFGZAEZHIZJKZALMZB
      VIHIZJKZALMVGNOZBLOCVHBCBPVHBKZVKVNALVPVJVMJVHBVIHQRSVODEZNOZVGVQTIZFKZUA
      ZDUBVLVOWAVLDVOWAVRVHVQFGZHIZJKZUAZVLVOVRWDVTVOVRWDVTUCVOVRUAZWDVSFGZJKZV
      TWFWCWGJVGVQUDRWHWGFFGZKVTJWIWGUEUFVSFVGVQTUGUHUIUJUKULWEVILOZVKUAZAUBZVL
      WBLOZWDWLVRWKWMWDUAAWBVQFUMVIWBKZWJWMVKWDVIWBLUNWNVJWCJVIWBVHHUORUPUQVQUR
      USVKALUTVAVBVCDVGVDVEVF $.
      $([15-May-96]$)
  $}

  ${
    $d x y A $.  $d x y z $.  $( ` y z ` are dummy. $)
    $( Existence of reciprocal of nonzero real number. $)
    axrrecex $p |- ( A e. RR ->
                   ( A =/= 0 _10 -> E. x e. RR ( A x. x ) = 1 _10 ) ) $=
      ( vy vz cv c0r cop cn0 wne cmulc co cn1 wceq cr wrex wi cnr wcel elreal
      neeq1 opreq1 cleq1d birexdv bi2imd wn wa wex cmr c1r visset recexsr
      wb mulresr df-1 cleq2i oprex eqresr bitr syl6bb exp pm5.32d opelreal
      biran syl5bb opex cleq3 opreq2 bi2and cla4ev syl6bir 19.23adv syld
      df-0 bineg syl5ib df-ne df-rex 3imtr4g gencl ) CEZFGZHIZWAAEZJKZLMZANOZPB
      HIZBWCJKZLMZANOZPVTQRZBNRCWABCBSWABMZWBWGWFWJWABHTWLWEWIANWLWDWHLWABWCJUA
      UBUCUDWKWAHMZUEZWCNRZWEUFZAUGZWBWFWKVTFMZUEZWQWNWKWSDEZQRZVTWTUHKZUIMZUFZ
      DUGWQDVTCUJZUKWKXDWQDWKXDWTFGZNRZWAXFJKZLMZUFZWQWKXAXIUFXDXJWKXAXIXCWKXAX
      IXCULWKXAUFZXIXBFGZLMZXCXKXHXLLVTWTDUJUMUBXMXLUIFGZMXCLXNXLUNUOXBUIVTWTUH
      UPUQURUSUTVAXGXAXIWTVBVCVDWPXJAXFWTFVEWCXFMZWOXGWEXIWCXFNVFXOWDXHLWCXFWAJ
      VGUBVHVIVJVKVLWMWRWMWAFFGZMWRHXPWAVMUOVTFXEUQURVNVOWAHVPWEANVQVRVS $.
      $([15-May-96]$)
  $}

  $( i-squared equals -1 (expressed as i-squared plus 1 is 0). $)
  axi2m1 $p |- ( ( i x. i ) + 1 _10 ) = 0 _10 $=
      ( c0r c1r cop cmulc co caddc cni cn1 cn0 cm1r cplr cmr cnr wcel wa wceq
      mulcnsr 0r 1r pm3.2i mp2an oprex elisseti opth 00sr ax-mp 1idsr opreq2i
      m1r eqtr opreq12i addcomsr 0idsr 3eqtr mpbir opreq1i addresr eqresr
      m1p1sr df-i df-1 df-0 3eqtr4 ) ABCZVDDEZBACZFEZAACZGGDEZHFEIVGJACZVFFEZJB
      KEZACZVHVEVJVFFVEAALEZJBBLEZLEZKEZBALEZABLEZKEZCZVJAMNZBMNZOZWDVEWAPABABQ
      WBWCRSTZWEUAWAVJPVQJPZVTAPZOVQVTJAVNVPKUBVRVSKUBAMRUCZUDWFWGVQAJKEJAKEZJV
      NAVPJKWBVNAPAUERUFVPJBLEZJVOBJLWCVOBPBUGSUFUHJMNZWJJPJUGUIUFUJUKAJWHJMUIU
      CULWKWIJPJUMUIUFUNVTAAKEZAVRAVSAKWCVRAPBUESUFWBVSAPAUGRUFUKWBWLAPAUMRUFUJ
      TUOUJUPWKWCVKVMPJBUQUISUAVMVHPVLAPVLAJBKUBURUSUOUNVIVEHVFFGVDGVDDUTUTUKVA
      UKVBVC $.
      $([5-May-96]$)

  $( Ordering on reals satisfies strict trichotomy. $)
  axlttri $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A < B <-> -. ( A = B \/ B < A ) ) ) $=
      ( cr clt wor wcel wa wbr wceq wo wn wb sotric ltsor mpan ) CDEACFBCFGABDHAB
      IBADHJKLCABDMNO $.
      $([19-May-96]$)

  $( Ordering on reals is transitive. $)
  axlttrn $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A < B /\ B < C ) -> A < C ) ) $=
      ( cr clt wor wcel w3a wbr wa wi sotr ltsor mpan ) DEFADGBDGCDGHABEIBCEIJAC
      EIKDABCELMN $.
      $([19-May-96]$)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
      $( ` x y z ` are dummy. $)
    $( Ordering property of addition on reals. $)
    axltadd $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( A < B -> ( C + A ) < ( C + B ) ) ) $=
      ( vx vy vz cr wcel w3a clt wbr caddc co cv c0r cop wb cnr elreal wceq
      breq1 opreq2 breq1d bi2bid breq2 breq2d opreq1 breq12d bilbid wa
      cltr cplr visset ltasr adantr ltresr a1i anandi addresr breqan12d sylbi
      oprex syl6bb 3bitr4d ancoms 3impa 3gencl biimpd ) AGHBGHCGHIABJKZCALMZC
      BLMZJKZDNZOPZENZOPZJKZFNZOPZVNLMZVSVPLMZJKZQZAVPJKZVSALMZWAJKZQVIWEVSBLMZ
      JKZQVIVLQDEFVNVPVSARGBCDASEBSFCSVNATZVQWDWBWFVNAVPJUAWIVTWEWAJVNAVSLUBUCU
      DVPBTZWDVIWFWHVPBAJUEWJWAWGWEJVPBVSLUBUFUDVSCTZWHVLVIWKWEVJWGVKJVSCALUGVS
      CBLUGUHUIVMRHZVORHZVRRHZWCWNWLWMUJZWCWNWOUJZVMVOUKKZVRVMULMZVRVOULMZUKKZV
      QWBWNWQWTQWOVMVOVRDUMZEUMZUNUOVQWQQWPVMVOXAXBUPUQWPWBWROPZWSOPZJKZWTWPWNW
      LUJZWNWMUJZUJWBXEQWNWLWMURXFXGVTXCWAXDJVRVMUSVRVOUSUTVAWRWSVRVMULVBVRVOUL
      VBUPVCVDVEVFVGVH $.
      $([11-May-96]$)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The product of two positive reals is positive. $)
    axmulgt0 $p |- ( ( A e. RR /\ B e. RR ) ->
                  ( ( 0 _10 < A /\ 0 _10 < B ) -> 0 _10 < ( A x. B ) ) ) $=
      ( vx vy cn0 cv c0r cop clt wbr wa cmulc co wi cnr cr elreal wceq breq2
      birand opreq1 breq2d bi2imd biland opreq2 wcel cmr cltr df-0 a1i
      visset mulresr breq12d 0r elisseti oprex ltresr syl6bb breq1i bitr
      bi2an mulgt0sr sylbi syl5bir 2gencl ) ECFZGHZIJZEDFZGHZIJZKZEVGVJLMZIJZNE
      AIJZVKKZEAVJLMZIJZNVOEBIJZKZEABLMZIJZNCDVGVJABOPCAQDBQVGARZVLVPVNVRWCVHVO
      VKVGAEISTWCVMVQEIVGAVJLUAUBUCVJBRZVPVTVRWBWDVKVSVOVJBEISUDWDVQWAEIVJBALUE
      UBUCVFOUFVIOUFKZVNGVFVIUGMZUHJZVLWEVNGGHZWFGHZIJWGWEEWHVMWIIEWHRWEUIUJVFV
      IDUKZULUMGWFGOUNUOZVFVIUGUPUQURVLGVFUHJZGVIUHJZKWGVHWLVKWMVHWHVGIJWLEWHVG
      IUIUSGVFWKCUKZUQUTVKWHVJIJWMEWHVJIUIUSGVIWKWJUQUTVAVFVIWNWJVBVCVDVE $.
      $([13-May-96]$)
  $}

  ${
    $d x y z w A $.  $( ` z w ` are dummy. $)
    $( A complex number can be expressed in terms of two reals.  Definition
       10-1.1(v) of Gleason, p. 130. $)
    axcnre $p |- ( A e. CC -> E. x e. RR E. y e. RR A = ( x + ( y x. i ) ) ) $=
      ( vz vw cv cop cni cmulc co caddc wceq cr wrex cnr cc df-c cleq1 birexdv
      wcel wa wex c0r opelreal bi2an biimpr cplr c1r cmr cm1r mulcnsr 0r 1r
      pm3.2i mpan2 00sr opreq1d 1idsr ax-mp opreq2i m1r eqtr 0idsr syl6eq
      opreq2d opreq1i elisseti visset addcomsr syl5eq eqtrd jca opeq12 syl
      df-i adantl addcnsr mpan12 mpan21 syl2an 3eqtrd cleqcomd opex cleq3
      bi2anan9 opreq1 sylan9eq cleq2d bi2and cla4e2v r2ex sylibr optocl ) DFZEF
      ZGZAFZBFZHIJZKJZLZBMNZAMNZCWTLZBMNZAMNDECOOPQWPCLZXBXEAMXFXAXDBMWPCWTRSSW
      NOTZWOOTZUAZWQMTZWRMTZUAZXAUAZBUBAUBZXCXIWNUCGZMTZWOUCGZMTZUAZWPXOXQHIJZK
      JZLZUAZXNXIXSYBXSXIXPXGXRXHWNUDWOUDUEUFXIYAWPXIYAXOUCWOGZKJZWNUCUGJZUCWOU
      GJZGZWPXHYAYELXGXHXTYDXOKXHXQUCUHGZIJZYDXTXHYJWOUCUIJZUJUCUHUIJZUIJZUGJZU
      CUCUIJZWOUHUIJZUGJZGZYDXHUCOTZYJYRLZXHYSUAYSUHOTZUAYTWOUCUCUHUKYSUVAULUMU
      NUOULUOXHYNUCLZYQWOLZUAYRYDLXHUVBUVCXHYNUCYMUGJZUCXHYKUCYMUGWOUPUQUVDUCUC
      UGJZUCYMUCUCUGYMUJUCUIJZUCYLUCUJUIYSYLUCLUCURULUSUTUJOTUVFUCLUJUPVAUSVBUT
      YSUVEUCLUCVCULUSVBVDXHYQYOWOUGJZWOXHYPWOYOUGWOURVEXHWOUCUGJZWOUVGWOVCZUVG
      YGUVHYOUCWOUGYSYOUCLUCUPULUSVFUCWOUCOULVGEVHVIZVBVJVKVLYNYQUCWOVMVNVKHYIX
      QIVOUTVJVEVPXGYSXHYEYHLZXGYSYSXHUAUVKWNUCUCWOVQULVRULVSYFWNLYGWOLYHWPLXGX
      HYFYGWNWOVMWNVCXHUVHWOYGUVIUVJVJVTWAWBVLXMYCABXOXQWNUCWCWOUCWCWQXOLZWRXQL
      ZUAZXLXSXAYBUVLXJXPUVMXKXRWQXOMWDWRXQMWDWEUVNWTYAWPUVLUVMWTXOWSKJYAWQXOWS
      KWFUVMWSXTXOKWRXQHIWFVEWGWHWIWJVNXAABMMWKWLWM $.
      $([13-May-96]$)
  $}

  ${
    $d x y z w v A $. $( ` w v ` are dummy. $)
    $( A non-empty, bounded-above set of reals has a supremum. $)
    axsup $p |- ( ( A (_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y < x ) ->
                E. x e. RR ( A. y e. A -. x < y /\
                A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( vw vv cr wss c0 wne cv clt wbr wral wrex wn wi wa wceq wcel wal wex
      ssel syl4d pm2.43 syl6 19.21aiiv df-ral syl6ibr pm3.27 19.22i df-rex
      sylibr syl3 19.20i a1i im2and jcab bial 19.26 bitr syl5ib imland 19.22dv
      adantr c0r cop cab ax-17 weq opeq1 cleq3d cbvab supre syl5 anabsi5 ax-1
      sylbi imlan sylan5 df-ne bilan sylanb 3impa ) DGHZDIJZBKZAKZLMZBDNZAGOZWH
      WGLMPZBDNZWIWGCKZLMZCDOZQZBGNZRZAGOZWEDISPZRZWKWTWEWFRXBWHGTZWGGTZWGDTZWI
      QZQZBUAZRZAUBZWTWKXBXJWTXBXCXDXEWLQZWIWNGTZWNDTWORZRZCUBZQZRQZBUAZRZAUBZW
      TXBXJRWEXTWTQXAWEXTXCWSRZAUBWTWEXSYAAWEXRWSXCWEXDXKQZBUAZXDXPQZBUAZRZWSXR
      WEYCWMYEWRWEYCXKBUAWMWEYBXKBWEYBXEXKQXKWEXEXDXKDGWGUCUDXEWLUEUFUGWLBDUHUI
      YEWRQWEYEXDWQQZBUAWRYDYGBXPWQXDXOWPWIXOXMCUBWPXNXMCXLXMUJUKWOCDULUMUNUNUO
      WQBGUHUMUPUQXRYBYDRZBUAYFXQYHBXDXKXPURUSYBYDBUTVAVBVCVDWSAGULUIVEABCEDFKZ
      VFVGZDTZFVHYKEKZVFVGZDTZFEYKEVIYNFVIFEVJYJYMDYIYLVFVKVLVMVNVOVPWKXCWJRZAU
      BXJWJAGULYOXIAWJXHXCWJXFBUAXHWIBDUHXFXGBXFXDVQUOVRVSUKVRVTWFXAWEDIWAWBWCW
      D $.
      $([19-May-96]$)
  $}

