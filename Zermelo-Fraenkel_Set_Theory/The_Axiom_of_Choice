$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f g h $.  $( ` u g h ` are dummy. $)
    ac3a.1 $e |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $.
    $( Derivation of our Axiom of Choice from a standard textbook version,
       which we express as the hypothesis.  This proof does not of course make
       use of our AC.  The conclusion is our AC in a modified form; the exact
       equivalence can be established by chaining our conclusion with the
       penultimate steps of proofs of ~ ac2 and ~ ac3 (which do not use AC to
       derive their penultimate steps).  Note that the Axiom of Regularity is
       used by the proof.  Specifically, ~ eirrv and ~ preleq that are
       referenced in the proof each make use of Regularity for their
       derivations. (The reverse derivation can be done without using
       Regularity; see ~ ac4 .) $)
    ac3a $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu vg vh cv c0 wceq wn cfv wcel wi wral wex wel wa wrex wreu cpr cab
      hbra1 wmo ra4 fvex eleq1 biland birexdv bi2and cla4ev weq eqid cleq1
      binegd rcla4ev mpan22 fveq2 preq1 syl preq2 eqtrd imlan r19.22si
      zfpaircl cleq2 elab sylibr visset pri2 pri1 pm3.2i jctir eleq2 sylan5
      exp syl8 imp3a pm2.43d wal eleq1d bitrd bi2imd rcla4v wb prel12 ancom
      syl6bb sylan9bbr eirrv mtbii con2 adantrr pm5.32d preleq syl6bir cleq2d
      biimparc syl6 exp4c com13 com4r imp imp4a com3l r19.23aiv sylbi exp3a
      imp4b 19.23adv df-rex syl5ib 19.21aiv mo2icl a1d jcad weu df-reu eu5
      bitr syl6ibr r19.21ai copab cvv fvopab cleq1i birex biabi fvrexex
      eqeltrr pm3.27 ssab2i ssexi rexeq bireudv bilimd biraldv 19.23aiv ax-mp
      ) CKZLMZNZUVCFKZOZUVCPZQZCAKZRZFSUVECETZDETZUAZEBKZUBZDUVCUCZQZCUVJRZBSZG
      UVKUVTFUVKUVEUVNEHKZLMZNZUWAUVFOZUWAUDZIKZMZUAZHUVJUBZIUEZUBZDUVCUCZQZCUV
      JRZUVTUVKUWMCUVJUVICUVJUFUVKCATZUVEUWLUVKUWOUVEUAZDCTZUWKUAZDSZUWRDUGZUAZ
      UWLUVKUWPUWSUWTUVKUWPUWSUVKUWOUVEUWPUWSQZUVKUWOUVEUVHUXBUVICUVJUHUVHUWPUW
      SUVHUVLUVGEKZPZUAZEUWJUBZUWSUWPUWRUVHUXFUADUVGUVCUVFUIZDKZUVGMZUWQUVHUWKU
      XFUXHUVGUVCUJUXIUVNUXEEUWJUXIUVMUXDUVLUXHUVGUXCUJUKULUMUNUWPUVGUVCUDZUWJP
      ZUVCUXJPZUVGUXJPZUAZUAUXFUWPUXKUXNUWPUWCUWEUXJMZUAZHUVJUBZUXKUWPUWCHCUOZU
      AZHUVJUBZUXQUWOUVECCUOZUXTCUPUXSUVEUYAUAHUVCUVJUXRUWCUVEUXRUYAUXRUWBUVDUW
      AUVCLUQURUWAUVCUVCUQUMUSUTUXSUXPHUVJUXRUXOUWCUXRUWEUVGUWAUDZUXJUXRUWDUVGM
      UWEUYBMUWAUVCUVFVAUWDUVGUWAVBVCUWAUVCUVGVDVEVFVGVCUWIUXQIUXJUVGUVCVHUWFUX
      JMZUWHUXPHUVJUYCUWGUXOUWCUWFUXJUWEVIUKULVJVKUXLUXMUVGUVCCVLZVMUVGUVCUXGVN
      VOVPUXEUXNEUXJUWJUXCUXJMUVLUXLUXDUXMUXCUXJUVCVQUXCUXJUVGVQUMUSVCVRVSVTWAW
      BUVKUWTUWPUVKUWRUXIQZDWCUWTUVKUYEDUVKUWQUWKUXIUVKUWQUWKUXIQUVKUWQUAZUXCUW
      JPZUVNUAZESUXIUWKUYFUYHUXIEUVKUWQUYGUVNUXIUYGUWQUVKUVNUXIQZUYGUWQUVKUYIUY
      GUWCUWEUXCMZUAZHUVJUBZUWQUVKUAZUYIQZUWIUYLIUXCEVLIEUOZUWHUYKHUVJUYOUWGUYJ
      UWCUWFUXCUWEVIUKULVJUYKUYNHUVJUYMHATZUYKUYIUYMUYPUWCUYJUYIUWQUVKUYPUWCUYJ
      UYIQZQQUVKUYPUWCUWQUYQUVKUYPUWCUWDUWAPZUWQUYQQUVIUWCUYRQCUWAUVJCHUOZUVEUW
      CUVHUYRUYSUVDUWBUVCUWALUQURUYSUVHUWDUVCPUYRUYSUVGUWDUVCUVCUWAUVFVAZWDUVCU
      WAUWDVQWEWFWGUYJUWQUYRUYIUYJUWQUYRUVNUXIUYJUWQUYRUAZUVNUAZUXHUWDMZUYSUAZU
      XIUYJVUBVUAUXHUVCUDUWEMZUAVUDUYJVUAVUEUVNUYJVUAVUEUVNWHZUYJUWQVUFUYRUYJDC
      UOZNZVUFUWQVUHVUEUXHUWEPZUVCUWEPZUAZUYJUVNUXHUVCUWDUWADVLZUYDUWAUVFUIZHVL
      ZWIUYJVUKUVMUVLUAUVNUYJVUIUVMVUJUVLUWEUXCUXHVQUWEUXCUVCVQUMUVMUVLWJWKWLVU
      GUWQVUGDDTUWQDWMUXHUVCUXHVQWNWOVRWPVSWQUXHUVCUWDUWAVULUYDVUMVUNWRWSUYSUXI
      VUCUYSUVGUWDUXHUYTWTXAXBXCXDVTXEXFXGXHXIXJXKXDXLXMUVNEUWJXNXOVSWAXPUWRDUV
      GXQVCXRXSUWLUWRDXTUXAUWKDUVCYAUWRDYBYCYDXKYEUVSUWNBUWJUWJUWGHUVJUBZIUEZUW
      AJKUXCUVFOZUXCUDZMEJYFZOZUWFMZHUVJUBZIUEVUPYGVVBVUOIVVAUWGHUVJVUTUWEUWFEJ
      UWAVURUWEVUNUWDUWAVHEHUOZVURUWDUXCUDZUWEVVCVUQUWDMVURVVDMUXCUWAUVFVAVUQUW
      DUXCVBVCUXCUWAUWDVDVEYHYIYJYKHIUVJVUSAVLYLYMUWIVUOIUWHUWGHUVJUWCUWGYNVGYO
      YPUVOUWJMZUVRUWMCUVJVVEUVQUWLUVEVVEUVPUWKDUVCUVNEUVOUWJYQYRYSYTUNVCUVAUVB
      $.
      $([17-Oct-96]$)
  $}

  ${
    $d x y z w v u $.
    $( Axiom of Choice using abbreviations.  This cute and very short version
       does not make use of any defined objects such as the empty set or a
       function value.  However, it is hard to explain intuitively.  If you
       want to figure it out, the equivalent ~ ac3 may be easier to understand.
       Note:  The penultimate step of the proof shows the logical equivalence
       to our ~ axac . $)
    ac2 $p |- E. y A. z e. x A. w e. z E! v e. z E. u e. y
              ( z e. u /\ v e. u ) $=
      ( wel wa wex weq wb wal wi cv wrex wreu wral axac 19.21v impexp bi2.04
      bitr bial weu df-eu df-reu 19.42v an41r3 anass bitr3 biex df-rex eleq1
      eleq2 bi2and cbvexv bilan 3bitr4 birbi bilim df-ral ax-17 hba1 hbex hbim
      birimd cbval 19.5 mpbi ) ECGZCAGZHZVLEAGZABGZHHZAIZEAJZKZELZAIZMZCLELZBIC
      FGZEFGZHZFBNZOZECNZPZDWHQZCANZQZBIBECARWBWLBWAELZCLVKWJMZCLWBWLWMWNCVKVJV
      TMZMZELVKWOELZMWMWNVKWOESWAWPEWAVJVKVTMMWPVJVKVTTVJVKVTUAUBUCWJWQVKDCGZWI
      MZDLWRVTMZDLWJWQWSWTDWIVTWRVJWGHZEUDXAVQKZELZAIWIVTXAEAUEWGEWHUFVSXCAVRXB
      EVPXAVQVJVNVKVMHZHZHZAIVJXEAIZHVPXAVJXEAUGVOXFAVOVJVNHXDHXFVJVNVKVMUHVJVN
      XDUIUJUKWGXGVJWGFBGZWEHZFIXGWEFWFULXIXEFAFAJZXHVNWEXDFNZWKWFUMXJWCVKWDVMX
      KWKWHUNXKWKENZUNUOUOUPUBUQURUSUCUKURUTUCWIDWHVAWOWTEDWODVBWRVTEWREVBVSEAV
      REVCVDVEEDJVJWRVTXLDNWHUMVFVGURUTURUCWAECVHWJCWKVAURUKVI $.
      $([18-Jul-96]$)
  $}

  ${
    $d x y z w v u $.  $( ` u ` is dummy. $)

    $( Axiom of Choice using abbreviations.  Note:  The penultimate step of the
       proof shows the logical equivalence to ~ ac2 .

       The following is a sketch to try to help you understand this version of
       the axiom.  We are given an arbitrary set ` x ` . The axiom tells that
       there exists a set ` y ` that we construct from ` x ` as follows.  For
       each non-empty member ` z ` of ` x ` , we choose a unique element ` w `
       from ` z ` .  Then we construct a set ` v ` containing both that ` w `
       as well as the set ` z ` itself.  (The set ` v ` may contain other stuff
       too, but we don't care.)  The final set ` y ` whose existence we
       postulate contains all of these ` v `'s.  (The set ` y ` may contain
       other stuff too, but we don't care.) $)
    ac3 $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu wel wa cv wrex wreu wral wex c0 wceq wn wi ac2 wal 19.23v df-ral
      nnull birim 3bitr4 biral biex mpbi ) CEGDEGHEBIJDCIZKZFUHLZCAIZLZBMUHNOPZ
      UIQZCUKLZBMABCFDERULUOBUJUNCUKFCGZUIQFSUPFMZUIQUJUNUPUIFTUIFUHUAUMUQUIFUH
      UBUCUDUEUFUG $.
      $([19-Jul-96]$)
  $}

  ${
    $d x z f y w v u g $.  $( ` y w v u g ` are dummy. $)
    $( Equivalent of Axiom of Choice.  We do not insist that ` f ` be a
       function.  However, theorem ~ ac5 , derived from this one, shows that
       this form of the axiom does imply that at least one such set ` f ` whose
       existence we assert is in fact a function.  Axiom of Choice of
       [TakeutiZaring] p. 83.  $)
    ac4 $p |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $=
      ( vv vw vy vu vg cv c0 wceq wn wel wa wrex wreu wi wral wex cfv wcel ac3
      crab cuni copab weq cab eleq2 eleq1 birand birexdv bi2and biabdv df-rab
      3eqtr4g unieqd cleqid visset zfausrab zfun2 opabval2 eleq1d reucl
      syl5bir syl3d r19.20i wmo moeq a1i funopabex fveq1 bilimd biraldv cla4ev
      syl 19.23aiv ax-mp ) BIZJKLZBDMZEDMZNZDFIZOZEVRPZQZBAIZRZFSVSVRCIZTZVRUAZ
      QZBWGRZCSZAFBEDUBWHWNFWHVSVRGAMZHIGDMZWANZDWCOZEGIZUCZUDZKZNGHUEZTZVRUAZQ
      ZBWGRZWNWFXFBWGBAMZWEXEVSXHXEWDEVRUCZUDZVRUAWEXHXDXJVRGHVRXAXJWGXCGBUFZWT
      XIXKEGMZWRNZEUGEBMZWDNZEUGWTXIXKXMXOEXKXLXNWRWDWSVREIUHXKWQWBDWCXKWPVTWAW
      SVRDIUIUJUKULUMWREWSUNWDEVRUNUOUPXCUQXIWDEVRBURUSUTVAVBWDEVRVCVDVEVFWMXGC
      XCXBGHWGAURXBHVGWOHXAVHVIVJWIXCKZWLXFBWGXPWKXEVSXPWJXDVRVRWIXCVKVBVLVMVNV
      OVPVQ $.
      $([21-Jul-96]$)
  $}

  ${
    $d x y f A $.  $( ` y ` is dummy. $)
    ac4c.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice (class version) $)
    ac4c $p |- E. f A. x e. A ( -. x = (/) -> ( f ` x ) e. x ) $=
      ( vy cv c0 wceq wn cfv wcel wi wral wex raleq biexdv ac4 vtoclbv ) AFZGHI
      SCFJSKLZAEFZMZCNTABMZCNEBDUABHUBUCCTAUABOPEACQR $.
      $([10-Feb-97]$)
  $}

  ${
    $d f x y w g A $.  $( ` y w g ` are dummy. $)
    ac5.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice.  Axiom AC of [BellMachover] p. 488. $)
    ac5 $p |- E. f ( f Fn A /\ A. x e. A ( -. x = (/) -> ( f ` x ) e. x ) ) $=
      ( vy vw vg cv c0 wceq wn cfv wcel wi wral wex wfn wa ac4c copab fveq2
      cleqid fvex opabval2 eleq1d bilimd birala bilan weu eueq1 a1i fnopab
      mpbiran wmo moeq funopabex fneq1 fveq1 biraldv bi2and cla4ev sylbir
      19.23aiv ax-mp ) AHZIJKZVEEHZLZVEMZNZABOZEPCHZBQZVFVEVLLZVEMZNZABOZRZCPZA
      BEDSVKVSEVKFHZBMZGHVTVGLZJZRFGTZBQZVFVEWDLZVEMZNZABOZRZVSWJWEVKWIVKWEWHVJ
      ABVEBMZWGVIVFWKWFVHVEFGVEWBVHBWDVTVEVGUAWDUBZVEVGUCUDUEUFUGUHWCFGBWDWCGUI
      WAGWBVTVGUCUJUKWLULUMVRWJCWDWCFGBDWCGUNWAGWBUOUKUPVLWDJZVMWEVQWIBVLWDUQWM
      VPWHABWMVOWGVFWMVNWFVEVEVLWDURUEUFUSUTVAVBVCVD $.
      $([29-Aug-99]$)
  $}

  ${
    $d x f A $.
    ac5b.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice. $)
    ac5b $p |- ( A. x e. A -. x = (/) ->
               E. f ( f : A --> U. A /\ A. x e. A ( f ` x ) e. x ) ) $=
      ( cv c0 wceq wn wral wfn cfv wcel wi wa wex cuni wf ac5 19.42v crn wss
      chfnrn exp anc2li df-f syl6ibr impac r19.26 pm3.35 r19.20si sylbir
      sylan5 an1s 19.22i mpan2 ) AEZFGHZABIZCEZBJZUQUPUSKUPLZMZABIZNZCOZBBPZUSQ
      ZVAABIZNZCOZABCDRURVENURVDNZCOVJURVDCSVKVICUTURVCVIUTVHVIURVCNZUTVHVGUTVH
      UTUSTVFUAZNVGUTVHVMUTVHVMABUSUBUCUDBVFUSUEUFUGVLUQVBNZABIVHUQVBABUHVNVAAB
      UQVAUIUJUKULUMUNUKUO $.
      $([31-Aug-99]$)
  $}

  ${
    $d f g x y z w A $.  $d f g x y z w B $.  $d ph z w f $.  $d y ps $.
    ac6.1 $e |- A e. V $.
    ac6.2 $e |- B e. V $.
    ${
      $d z f g C $.  $d z f g H $.
      ac6lem.4 $e |- C = { y e. B | ph } $.
      ac6lem.5 $e |- H = { <. x , z >. | ( x e. A /\ z = C ) } $.
      $( Lemma for equivalent of Axiom of Choice. $)
      ac6lem $p |- ( A. x e. A E. y e. B ph ->
                   E. f ( f : A --> B /\ A. x e. A ( f ` x ) e. C ) ) $=
        ( vg wrex wral cv c0 wceq wn crn cuni wf cfv wcel wa wex wi crab
        cleq2i biimp cleq1d binegd rnoel3 syl6bb biimprcd r19.20si r19.23v
        sylib cab abid copab rneq ax-mp rnopab eqtr eleq2i df-rex 3bitr4
        syl5ib r19.21aiv cvv wfn weu zfausrab eqeltr eueq1 a1i fnopab fnex mp2
        rnexg ac5b ccom fnfrn mpbi fco mpan2 adantr wfun ax-17 wal hbopab1
        bial 3imtr4 hbrn hbral hban fveq2 id eleq12d rcla4v isseti 19.8a
        cleqabi sylibr exp comm eleq1 sylibd 19.23aiv syl5 imp adantll wb
        fnfun cdm fvco fndm sylan5br mpan12 fvopab2 fveq1i syl5eq fveq2d
        adantl eqtrd eleq1d adantlr mpbird r19.21ai ffun sylan jca wss ssuniss
        ssrab eqsstr sseq1 mpbiri r19.23aiv sylbi mpg fss visset coex feq1 wel
        hbco hbeq fveq1 birald bi2and cla4ev syl 3syl ) ACFOZBEPZDQZRSZTZDIUAZP
        UWBUWBUBZNQZUCZUVSUWDUDZUVSUEZDUWBPZUFZNUGEFHQZUCZBQZUWJUDZGUEZBEPZUFZH
        UGZUVRUWADUWBUVRUVSGSZBEOZUWAUVSUWBUEZUVRUWRUWAUHZBEPUWSUWAUHUVQUXABEUW
        RUWAUVQUWRUWAACFUIZRSZTUVQUWRUVTUXCUWRUVSUXBRUWRUVSUXBSGUXBUVSLUJUKULUM
        ACFUNUOUPUQUWRUWABEURUSUVSUWLEUEZUWRUFZBUGZDUTZUEUXFUWTUWSUXFDVAUWBUXGU
        VSUWBUXEBDVBZUAZUXGIUXHSUWBUXISMIUXHVCVDUXEBDVEVFZVGUWRBEVHVIZVJVKDUWBN
        IVLUEZUWBVLUEEVLUEIEVMZUXLJUWRBDEIUWRDVNUXDDGGUXBVLLACFKVOVPZVQVRMVSZEV
        LIVTWAZIVLWBVDWCUWIUWQNUWIEFUWDIWDZUCZUWLUXQUDZGUEZBEPZUFZUWQUWBFUWDUCZ
        UWHUYBUWEUYCUWHUFUXRUYAUYCUXRUWHUYCEUWBIUCZUXRUXMUYDUXOEIWEWFEUWBFUWDIW
        GWHWIUWDWJZUWHUYAUYCUYEUWHUFZUXTBEUYEUWHBUYEBWKUWGBDUWBBDIUVSUXHUEZUYGB
        WLUVSIUEZUYHBWLUXEBDDWMIUXHUVSMVGZUYHUYGBUYIWNWOZWPUWGBWKWQWRUYFUXDUXTU
        YFUXDUFUXTGUWDUDZGUEZUWHUXDUYLUYEUWHUXDUYLUWHGUWBUEZUYLUXDUWGUYLDGUWBUW
        RUWFUYKUVSGUVSGUWDWSUWRWTXAXBUWRDUGUXDUYMUHZDGUXNXCUWRUYNDUWRUXDUWTUYMU
        XDUWRUWTUXDUWRUWTUXEUXFUWTUXEBXDUXFDUWBUXJXEXFXGXHUVSGUWBXIXJXKVDXLXMXN
        UYEUXDUXTUYLXOUWHUYEUXDUFZUXSUYKGUYOUXSUWLIUDZUWDUDZUYKUYEIWJZUXDUXSUYQ
        SZUXMUYRUXOEIXPVDUYEUYRUFUWLIXQZUEUYSUXDUWLUWDIXRUYTEUWLUXMUYTESUXOEIXS
        VDVGXTYAUXDUYQUYKSUYEUXDUYPGUWDUXDUWLUXHUDZGUYPUXDGVLUEVUAGSUXNBDEGVLYB
        WHUWLIUXHMYCYDYEYFYGYHYIYJXGYKUWBFUWDYLYMYNUWEUWCFYOZUYCUWTUVSFYOZUHVUB
        DDUWBFYPUWTUWSVUCUXKUWRVUCBEUWRVUCUHUXDUWRVUCGFYOGUXBFLACFYQYRUVSGFYSYT
        VRUVAUVBUVCUWBUWCFUWDUVDWHYMUWPUYBHUXQUWDINUVEUXPUVFUWJUXQSZUWKUXRUWOUY
        AEFUWJUXQUVGVUDUWNUXTBEBDDUWJUXQDHUVHBWKBDUWDIDNUVHBWKUYJUVIUVJVUDUWMUX
        SGUWLUWJUXQUVKYHUVLUVMUVNUVOXKUVP $.
        $([18-Oct-99]$)
    $}

    ${
      ac6.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
      $( Equivalent of Axiom of Choice.  This is useful for proving that there
         exists, for example, a sequence mapping natural numbers to members of
         a large set ` B ` , where ` ph ` depends on ` x ` (the
         natural number) and ` y ` (to specify a subset of ` B ` ). $)
      ac6 $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
        ( vz vw wrex wral cv wf cfv crab wcel wa wex wceq copab cleqid weq
        cleq1 biland cbvopab2v ac6lem elrab pm3.27bd r19.20si imlan 19.22i syl
        ) ADFMCENEFGOZPZCOZUPQZADFRZSZCENZTZGUAUQBCENZTZGUAACDKEFUTGURESZLOZUTU
        BZTZCLUCHIUTUDVIVFKOZUTUBZTCLKLKUEVHVKVFVGVJUTUFUGUHUIVCVEGVBVDUQVABCEV
        AUSFSBABDUSFJUJUKULUMUNUO $.
        $([18-Oct-99]$)
    $}
  $}

  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x ( x e. On /\ ( f Fn x /\
                A. y ( y e. x -> ( f ` y ) = ( G ` ( f |` y ) ) ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` P~ A ` . $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numthlem   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 wceq wn cfv wcel wi cpw wral wex wf1o con0 wrex zfpowcl ac4c
      cima cdif cres wa tfr2 crn copab cvv wfun visset tfrlem7 resfunexg mp2
      fvex rneq difeq2 fveq2 3syl fvopab fveq1i df-ima difeq2i fveq2i 3eqtr4
      syl6eq eleq1d wss difss ssexi elpw mpbir cleq1 binegd id eleq12d bi2imd
      rcla4v mpi imp syl5bir exp3a comm r19.21aiv tfr1 tz7.49c f1oeq1 cla4ev
      r19.22si 19.23aiv ax-mp ) BMZNOZPZWRFMZQZWRRZSZBCTZUAZFUBAMZCEMZUCZEUBZAU
      DUEZBXEFCIUFUGXFXKFXFCGXGUHZUIZNOZPZXGGQZXMRZSZAUDUAXGCGXGUJZUCZAUDUEXKXF
      XRAUDXGUDRZXFXRYAXFXOXQYAXQXMXAQZXMRZXFXOUKYAXPYBXMYAXPXSHQZYBABADEGHJKUL
      XSWRCXHUMZUIZXAQZOEBUNZQCXSUMZUIZXAQZYDYBEBXSYGYKXGUORGUPXSUORAUQABDEGHJK
      URGXGUOUSUTZYJXAVAXHXSOYEYIOYFYJOYGYKOXHXSVBYEYICVCYFYJXAVDVEVFXSHYHLVGXM
      YJXAXLYICGXGVHVIVJVKVLVMXFXOYCXFXMXERZXOYCSZYMXMCVNCXLVOZXMCXMCIYOVPVQVRX
      DYNBXMXEWRXMOZWTXOXCYCYPWSXNWRXMNVSVTYPXBYBWRXMWRXMXAVDYPWAWBWCWDWEWFWGWH
      WIWJACGABDEGHJKWKIWLXTXJAUDXIXTEXSYLXGCXHXSWMWNWOVEWPWQ $.
      $([10-Feb-97]$)
  $}

  ${
    $d x y z w v u f g h A $.  $( ` y z w v u g h ` are dummy. $)
    numth.1 $e |- A e. V $.
    $( Numeration theorem:  every set can be put into one-to-one correspondence
       with some ordinal (using AC).  Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vz vg vw vu vv vh cv con0 wcel wfn wel cfv cres crn cdif wceq copab
      wi wal wa wex cab cuni rdglem1 cleqid weq id rneq difeq2 fveq2 3syl
      cleqan12rd cbvopabv numthlem ) AEBFLZMNGLZUTOHFPHLZVAQVAVBRILZBJLZSZTZKLZ
      QZUAZJIUBZQUAUCHUDUEUEFUFGUGZCKVKUHZVJDFHAEGCVJUIVLUJVIELZBCLZSZTZVGQZUAJ
      ICEIEUKZJCUKZVCVMVHVQVRULVSVEVOUAVFVPUAVHVQUAVDVNUMVEVOBUNVFVPVGUOUPUQURU
      S $.
      $([10-Feb-97]$)
  $}

  ${
    $d x y z A $.
    $( Numeration theorem:  any set is equinumerous to some ordinal (using AC).
       Theorem 10.3  of [TakeutiZaring] p. 84. $)
    numth2 $p |- E. x e. On x ~~ A $=
      ( vy vz cvv wcel cv cen wbr con0 wrex wceq breq2 birexdv wf1o wex visset
      numth bren birex mpbir vtoclg wn c0 0elon enrefg ax-mp brprc mpbiri
      breq1 rcla4ev mpan syl pm2.61i ) BEFZAGZBHIZAJKZUPCGZHIZAJKZURCBEUSBLUTUQ
      AJUSBUPHMNVAUPUSDGODPZAJKAUSDCQZRUTVBAJUPUSDVCSTUAUBUOUCZUDBHIZURVDVEUDUD
      HIZUDJFZVFUEUDJUFUGUDBHUHUIVGVEURUEUQVEAUDJUPUDBHUJUKULUMUN $.
      $([20-Oct-03]$)
  $}

  ${
    $d x y A $.
    $( Any set is strictly dominated by some ordinal. $)
    numthcor $p |- ( A e. B -> E. x e. On A ~< x ) $=
      ( vy cv csdm wbr con0 wrex wceq breq1 birexdv cpw cen numth2 cvv wcel wi
      visset zfpowcl ensymg ax-mp canth2 wa sdomentr mpan syl r19.22si vtoclg
      ) DEZAEZFGZAHIZBUKFGZAHIDBCUJBJULUNAHUJBUKFKLUKUJMZNGZAHIUMAUOOUPULAHUPUO
      UKNGZULUOPQUPUQRUJDSZTUKUOPUAUBUJUOFGZUQULUJURUCUKPQUSUQUDULRASUJUOUKPUEU
      BUFUGUHUBUI $.
      $([11-Nov-03]$)
  $}

  ${
    $d x y z w f A $.  $( ` y z w f ` are dummy. $)
    weth.1 $e |- A e. V $.
    $( Well-ordering theorem: any set ` A ` can be well-ordered.  This is an
       equivalent of the Axiom of Choice.  Theorem 6 of [Suppes] p. 242. $)
    weth   $p |- E. x x We A $=
      ( vy vf vz vw cv wf1o wex con0 wrex wwe numth wcel wi ccnv f1ocnv cep
      cfv wbr copab cleqid f1owe cxp cin weinxp xpex zfauscl2 weeq1 cla4ev
      sylbi syl6 word eloni ordwe syl syl5 19.23aiv comm r19.23aiv ax-mp ) DHZB
      EHZIZEJZDKLBAHZMZAJZDBECNVFVIDKVFVCKOZVIVEVJVIPZEVEBVCVDQZIZVKVCBVDRVMVCS
      MZVIVJVMVNBFHVLTGHVLTSUAFGUBZMZVIFGBVCVOSVLVOUCUDVPBVOBBUEZUFZMZVIBVOUGVH
      VSAVRVQVOBBCCUHUIBVGVRUJUKULUMVJVCUNVNVCUOVCUPUQURUQUSUTVAVB $.
      $([9-Mar-97]$)
  $}


  ${
    $v s $.
    $v r $.
    $v a $.
    $v b $.
    zornlem.vs $f set s $.
    zornlem.vr $f set r $.
    zornlem.va $f set a $.
    zornlem.vb $f set b $.
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.
    $d h t f G $.
    $d t C $.  $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zornlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    zornlem.2 $e |- B = { f | E. h ( h e. On /\ ( f Fn h /\
                A. t ( t e. h -> ( f ` t ) = ( G ` ( f |` t ) ) ) ) ) } $.
    zornlem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zornlem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zornlem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A ` . $)
    zornlem.6 $e |- G =
       { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.

    $( Lemma for Zorn's lemma. $)
    zornlem1 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con0 wcel wwe c0 wceq wn wa cfv wbr wral crab cuni cres tfr2 copab
      fveq1i cvv wfun visset tfrlem7 resfunexg mp2 cima ssrab eqsstr ssexi
      zfausrab zfun2 cab crn rneq df-ima syl6eqr eleq2d birimd biraldv2
      birabsdv 3eqtr4g bi2and biabdv df-rab unieqd fvopab eqtr syl6eq eleq1d
      wreu wss wereu mpan21 reucl syl syl5bir imp ) AUCZUDUEZGCUCZUFZJUGUHUIZUJ
      ZWROUKZJUEZWSXEEUCZDUCZWTULUIZEJUMZDJUNZUOZJUEZXCWSXDXKJWSXDOWRUPZPUKZXKN
      FAHLOPRSUQXNXMFUCXHEIUMZDIUNZUOZUHLFURZUKXKXMPXRUBUSLFXMXQXKWRUTUEOVAXMUT
      UEAVBNFHLOPRSVCOWRUTVDVEXJXIDJJGQJMUCZBUCKULZMOWRVFZUMZBGUNZGUAYBBGVGVHZV
      IZVJVKLUCZXMUHZXPXJYGXGIUEZXOUJZDVLXGJUEZXIUJZDVLXPXJYGYIYKDYGYHYJXOXIYGI
      JXGYGXTMYFVMZUMZBGUNYCIJYGYMYBBGYGXTXTMYLYAYGXSYLUEXSYAUEXTYGYLYAXSYGYLXM
      VMYAYFXMVNOWRVOVPVQVRVSVTTUAWAZVQYGXHXHEIJYGXFIUEXFJUEXHYGIJXFYNVQVRVSWBW
      CXODIWDXIDJWDWAWEWFWGWHWIXCXIDJWJZXLXAJGWKXBYOYDDEGJWTYEWLWMXIDJWNWOWPWQ
      $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem2 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con0 wcel wwe c0 wceq wn wa wel cfv cima wbr wi cdm wss onsst wfn
      tfr1 fndm ax-mp sseq2i sylibr wfun tfrlem7 funfvima2 mpan syl adantr
      wral zornlem1 crab eleq2i breq2 biraldv elrab bitr pm3.27bd breq1 rcla4v
      3syl syld ) AUDZUEUFZHDUDUGKUHUIUJUKZUKZBAULZBUDZPUMZPWEUNZUFZWKWEPUMZLUO
      ZWFWIWMUPZWGWFWEPUQZURZWPWFWEUEURWRWEUSWQUEWEPUEUTWQUEUIOGIMPQSTVAUEPVBVC
      VDVEPVFWRWPOGIMPQSTVGWEWJPVHVIVJVKWHWNKUFZNUDZWNLUOZNWLVLZWMWOUPACDEFGHIJ
      KLMNOPQRSTUAUBUCVMWSWNHUFZXBWSWNWTCUDZLUOZNWLVLZCHVNZUFXCXBUKKXGWNUBVOXFX
      BCWNHXDWNUIXEXANWLXDWNWTLVPVQVRVSVTXAWONWKWLWTWKWNLWAWBWCWD $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ -. D = (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con0 wcel wwe c0 wceq wn wa wel cfv wbr wi zornlem2 adantl
      breq1 biimprcd poirr nsyli comm zornlem1 cima wral crab ssrab eqsstr ssi
      syl sylan5 syld ) HLUDZAUEZUFUGHDUEUHKUIUJUKULULZULBAUMZBUEPUNZVOPUNZLUOZ
      VSVRUJZUKZVPVQVTUPVNABCDEFGHIJKLMNOPQRSTUAUBUCUQURVNVSHUGZVTWBUPVPVTVNWCU
      LZWBVTWAVSVSLUOZWDWAWEVTVSVRVSLUSUTHVSLVAVBVCVPVSKUGWCACDEFGHIJKLMNOPQRST
      UAUBUCVDKHVSKNUECUELUONPVOVEVFZCHVGHUBWFCHVHVIVJVKVLVM $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa con0 wcel c0 wceq wex wrex crn cvv wn pm3.24 wi wal
      wss cfv ax-17 hba1 hban eleq1 zornlem1 wbr cima wral crab ssrab eqsstr
      ssi syl syl5bi comm exp32 a2d imp4a a4sd imp 19.23ad wfn wb tfr1 fvelrn
      ax-mp syl5ib ssrdv ssex exp adantl ccnv wfun wel zornlem3 exp45 com23
      19.21adv 19.20dv r2al syl6ibr cres ssid tz7.48lem mpan wrel cdm tfrlem6
      fndm relssres mp2an cnveq funeq sylibr syl6 onprc funrnex df-rn eleq1i
      dfdm4 eqtr3 3imtr4g mtoi jcad alnex syl5ibr mt3i df-rex ) GKUDZGCUEUFZUGZ
      AUEZUHUIZJUJUKZUGAULZYOAUHUMYLYPOUNZUOUIZYRUPZUGZYRUQYLYNYOUPZURZAUSZYTYP
      UPYLUVCYRYSYKUVCYRURYJYKUVCYRYKUVCUGZYQGUTYRUVDUCYQGUVDYNYMOVAZUCUEZUKZUG
      ZAULZUVFGUIZUVFYQUIZUVDUVHUVJAYKUVCAYKAVBUVBAVCVDUVJAVBYKUVCUVHUVJURZYKUV
      BUVLAYKUVBYNUVGUVJYKYNUVAUVGUVJURZYNYKUVAUVMURYNYKUVAUVMUVGYNYKUVAUGUGZUV
      JUVGUVEGUIZUVJUVNUVEUVFGVEUVNUVEJUIUVOABCDEFGHIJKLMNOPQRSTUAUBVFJGUVEJMUE
      BUEKVGMOYMVHVIZBGVJGUAUVPBGVKVLVMVNVOVPVQVPVRVSVTWAWBOUHWCZUVKUVIWDNFHLOP
      RSWEZAUHUVFOWFWGWHWIYQGQWJVNWKWLYLUVCOWMZWNZYSYLUVCUVEUVFOVAUKUPZUCYMVIAU
      HVIZUVTYLUVCYNUCAWOZUGUWAURZUCUSZAUSUWBYLUVBUWEAYLUVBUWDUCYLUVBYNUWCUWAYL
      YNUVAUWCUWAURZYJYKYNUVAUWFURZURYJYNYKUWGYJYNYKUVAUWFAUCBCDEFGHIJKLMNOPQRS
      TUAUBWPWQWRWAVRVSWSWTUWAAUCUHYMXAXBUWBOUHXCZWMZWNZUVTUHUHUTUWBUWJUHXDZAUC
      UHOUVRXEXFUVSUWIUKZUVTUWJWDOUWHUKZUWLOXGOXHZUHUTUWMNFHLOPRSXIUWNUHUHUVQUW
      NUHUKUVRUHOXJWGZUWKVLOUHXKXLOUWHXMWGUVSUWIXNWGXOXPUVTYRUHUOUIZXQUVTUVSXHZ
      UOUIZUVSUNZUOUIZYRUWPUWRUVTUWTUOUVSXRVPYQUWQUOOXSXTUHUWSUOUWNUHUWSUWOOYAY
      BXTYCYDXPYEYNYOAYFYGYHYOAUHYIXO $.
      $([3-Apr-97]$)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y ` . $)
      zornlem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for Zorn's lemma. $)
      zornlem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) ->
            ( F " x ) (_ A ) $=
        ( zornlem.vs cv wwe con0 wcel wa c0 wceq wn wral cima wel cfv wex
        ax-17 hbra1 hban wi wal eleq1 zornlem1 wbr crab ssrab eqsstr ssi syl
        syl5bi ontrt syland comm exp43 com13 imp a2d a4sd df-ral syl5ib imp4b
        19.23ad wfun tfrlem7 fvelima mpan syl5 ssrdv ) HDUGUHZAUGZUIUJZUKZRULUM
        UNZBWMUOZUKZUFPWMUPZHWRBAUQZBUGZPURZUFUGZUMZUKZBUSZXCHUJZXCWSUJZWRXEXGB
        WOWQBWOBUTWPBWMVAVBXGBUTWOWQWTXDXGWOWTWPVCZBVDWTXDXGVCZVCZWQWOXIXKBWOWT
        WPXJWLWNWTWPXJVCZVCWTWNWLXLWTWNWLWPXJXDWTWNUKZWLWPUKZUKXGXDXAUIUJZXNXGX
        MXDXBHUJZXGXOXNUKZXBXCHVEXQXBRUJXPBCDEFGHIJRLMNOPQSTUAUBUEUDVFRHXBRNUGC
        UGLVGNPXAUPUOZCHVHHUEXRCHVIVJVKVLVMXAWMVNVOVPVQVRVSVTWAWPBWMWBWCWDWEPWF
        XHXFOGIMPQTUAWGBXCWMPWHWIWJWK $.
        $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) -> R Or ( F " x ) ) ) $=
        ( zornlem.vr zornlem.vs zornlem.va zornlem.vb wpo cv wwe con0 wcel wa
        c0 wceq wn wral cima wbr weq w3o wor wss wi zornlem5 poss syl comm wal
        wel cfv wex crab anandir ontrt im2an sylbi ancoms exp cleqid zornlem2
        adantll breq12 biimpcd syl6 com23 pm3.27 imlan sylan5 imp cleq12 fveq2
        syl5bi adantl adantlr wb pm3.26 im3ord word ordtri3or eloni syl2an
        syl5 exp31 com4r pm2.43i exp4a com3r a2d cleq1i bineg biral imaeq2
        raleq birabsdv cleq1d binegd rcla4v im2and imp4b 19.23advv wfun wfn
        tfr1 fnfun ax-mp fvelima an4 bi2ex eeanv bitr sylibr 19.21aivv r2al
        a1i jcad df-so syl6ibr ) HLUJZHDUKULZAUKZUMUNZUOZRUPUQZURZBUVCUSZUOZPUV
        CUTZLUJZUFUKZUGUKZLVAZUFUGVBZUVMUVLLVAZVCZUGUVJUSUFUVJUSZUOUVJLVDUVAUVI
        UVKUVRUVIUVAUVKUVIUVJHVEUVAUVKVFABCDEFGHIJKLMNOPQRSTUAUBUCUDUEVGUVJHLVH
        VIVJUVIUVRVFUVAUVIUVLUVJUNZUVMUVJUNZUOZUVQVFZUGVKUFVKUVRUVIUWBUFUGUVIUH
        AVLZUIAVLZUOZUHUKZPVMZUVLUQZUIUKZPVMZUVMUQZUOZUOZUIVNUHVNZUVQUWAUVIUWMU
        VQUHUIUVEUVHUWEUWLUVQUVEUWENUKCUKLVAZNPUWFUTZUSZCHVOZUPUQZURZUWONPUWIUT
        ZUSZCHVOZUPUQZURZUOZVFZUWEUWLUVQVFZVFUVHUVEUWEUXFUXHUVBUVDUWEUXFUXHVFZV
        FUVDUWEUVBUXIUVDUWEUVBUXFUXHUVDUWEUWFUMUNZUWIUMUNZUOZUVBUXFUOZUXHVFZUVD
        UWEUXLUWEUVDUXLUWEUVDUOUWCUVDUOZUWDUVDUOZUOUXLUWCUWDUVDVPUXOUXJUXPUXKUW
        FUVCVQUWIUVCVQVRVSVTWAUXLUXNUXLUXMUWLUXLUVQUXLUXMUWLUXLUVQVFUXLUXMUOZUW
        LUOZUHUIVLZUHUIVBZUIUHVLZVCZUVQUXLUXRUXSUVNUXTUVOUYAUVPUXQUWLUXSUVNVFZU
        XLUVBUXEUOZUWLUYCVFUXMUXLUYDUOZUXSUWLUVNUYEUXSUWGUWJLVAZUWLUVNVFUXKUYDU
        XSUYFVFUXJUIUHCDEFGHIJUXCLMNOPQSTUAUBUXCWBUDWCWDUWLUYFUVNUWGUVLUWJUVMLW
        EWFWGWHUXFUXEUVBUWTUXEWIWJWKWLUWLUXTUVOVFUXQUWLUWGUWJUQUVOUXTUWGUVLUWJU
        VMWMUWFUWIPWNWOWPUXQUWLUYAUVPVFZUXLUVBUWTUOZUWLUYGVFUXMUXLUYHUOZUYAUWLU
        VPUYIUYAUWJUWGLVAZUWLUVPVFUXJUYHUYAUYJVFUXKUHUICDEFGHIJUWRLMNOPQSTUAUBU
        WRWBUDWCWQUWLUYJUVPUWKUWHUYJUVPWRUWJUVMUWGUVLLWEVTWFWGWHUXFUWTUVBUWTUXE
        WSWJWKWLWTUWFXAUWIXAUYBUXJUXKUWFUWIXBUWFXCUWIXCXDXEXFXGXHWGXIXJWLXKUVHU
        WONPBUKZUTZUSZCHVOZUPUQZURZBUVCUSZUXGUVGUYPBUVCUVFUYORUYNUPUEXLXMXNUYQU
        WCUWTUWDUXEUYPUWTBUWFUVCBUHVBZUYOUWSUYRUYNUWRUPUYRUYMUWQCHUYRUYLUWPUQUY
        MUWQWRUYKUWFPXOUWONUYLUWPXPVIXQXRXSXTUYPUXEBUWIUVCBUIVBZUYOUXDUYSUYNUXC
        UPUYSUYMUXBCHUYSUYLUXAUQUYMUXBWRUYKUWIPXOUWONUYLUXAXPVIXQXRXSXTYAVSXEYB
        YCUWAUWCUWHUOZUHVNZUWDUWKUOZUIVNZUOZUWNPYDZUWAVUDVFPUMYEVUEOGIMPQTUAYFU
        MPYGYHVUEUVSVUAUVTVUCVUEUVSVUAUHUVLUVCPYIWAVUEUVTVUCUIUVMUVCPYIWAYAYHUW
        NUYTVUBUOZUIVNUHVNVUDUWMVUFUHUIUWCUWDUWHUWKYJYKUYTVUBUHUIYLYMYNXEYOUVQU
        FUGUVJUVJYPYNYQYRUFUGUVJLYSYT $.
        $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem7 $p |- ( ( R Po A /\
      A. s ( ( s (_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
        ( wpo cv wss wor wa wbr weq wo wral wrex wi wal wwe wex wn weth c0
        wceq con0 zornlem4 cima crab wb imaeq2 raleq syl birabsdv 3eqtr4g
        cleq1d onminex wcel zornlem5 a1i zornlem6 jcad wfun cvv tfrlem7 visset
        zfrep5 ax-mp sseq1 soeq2 bi2and birexdv bi2imd cla4v sylan9 adantld
        imp noel ssd w3a potr 3anass sylan5br exp3a com23 breq1 biimprcd
        adantl jaod exp42 sylan9r com24 imp31 a2d 19.20dv df-ral 3imtr4g
        cleq1i eleq2 sylbi breq2 biraldv elrab syl5bbr biimpd cbvralv syl5ib
        exp32 com34 mtoi exp4a exp com4r pm2.43a imp3a com4l 19.21adv syl6ibr
        imdistan 19.22dv df-rex comm adantr imp32 mpd exp45 imp4a com3l
        r19.23aiv 3syl adantlr pm2.43i 19.23adv mpi ) HLUJZSUKZHULZUVRLUMZUNZTU
        KZUAUKZLUOZTUAUPZUQZTUVRURZUAHUSZUTZSVAZUNZHDUKVBZDVCUWCUBUKZLUOZVDZUBH
        URZUAHUSZDHUCVEUWKUWLUWQDUWKUWLUWQUWKUWLUNZUWQUVQUWLUWRUWQUTZUWJUVQUWLU
        NKVFVGZAVHUSUWTRVFVGZVDBAUKZURZUNZAVHUSUWSACDEFGHIJKLMNOPQUCUDUEUFUGUHV
        IUWTUXAABABUPZKRVFUXENUKZCUKZLUOZNPUXBVJZURZCHVKZUXHNPBUKZVJZURZCHVKKRU
        XEUXJUXNCHUXEUXIUXMVGUXJUXNVLUXBUXLPVMUXHNUXIUXMVNVOVPUGUIVQVRVSUXDUWSA
        VHUWRUXBVHVTZUXDUWQUWRUXOUWTUXCUWQUWKUWLUXOUWTUXCUWQUTZUTZUTUWKUWLUXOUX
        QUWKUWTUWLUXOUNZUXPUWKUWTUXRUXCUWQUWKUWTUXRUXCUNZUNZUNUWFTUXIURZUAHUSZU
        WQUWKUXTUYBUWKUXSUYBUWTUVQUXSUXIHULZUXILUMZUNZUWJUYBUVQUXSUYCUYDUXSUYCU
        TUVQABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWAZWBABCDEFGHIJKLMNOPQRUCUDUEUFUGUH
        UIWCWDUWIUYEUYBUTSUXIPWEUXIWFVTOGIMPQUDUEWGPUXBAWHWIWJUVRUXIVGZUWAUYEUW
        HUYBUYGUVSUYCUVTUYDUVRUXIHWKUVRUXILWLWMUYGUWGUYAUAHUWFTUVRUXIVNWNWOWPWQ
        WRWSUWKUWTUXSUYBUWQUTZUVQUWTUXSUYHUTZUTUWJUWTUVQUYIUWTUVQUXSUYHUWTUVQUX
        SUNZUNZUWCHVTZUYAUNZUAVCUYLUWPUNZUAVCUYBUWQUYKUYMUYNUAUYKUYLUYAUWPUTUTU
        YMUYNUTUYKUYLUYAUWPUYKUYMUWMHVTZUWOUTZUBVAUWPUYKUYMUYPUBUYKUYLUYAUYPUYO
        UYKUYLUYAUWOUYOUWTUYJUYLUYAUWOUTZUTZUWTUYOUYJUYRUTUWTUYOUYJUYOUYRUWTUYO
        UYJUYOUYRUTUTUWTUYOUNZUYJUYLUYOUYQUYSUYJUYLUYOUYQUYSUYJUYLUYOUNZUYAUWOU
        YSUYJUYTUNZUNUYAUNUWNUWMVFVTZUWMWTUYSVUAUYAUWNVUBUTUYSVUAUWNUYAVUBUYSVU
        AUWNUYAVUBUTVUAUWNUNZUYAUWBUWMLUOZTUXIURZUYSVUBVUCUWBUXIVTZUWFUTZTVAVUF
        VUDUTZTVAUYAVUEVUCVUGVUHTVUCVUFUWFVUDUYJUYTUWNVUFUWFVUDUTZUTZUYJUWNUYTV
        UJUYJVUFUYTUWNVUIUXSVUFUWBHVTZUVQUYTUWNVUIUTUTUXSUXIHUWBUYFXAUVQVUKUYTU
        WNVUIUVQVUKUYTUNZUNZUWNUNUWDVUDUWEVUMUWNUWDVUDUTVUMUWDUWNVUDVUMUWDUWNVU
        DUVQVUKUYLUYOXBUWDUWNUNVUDUTVULHUWBUWCUWMLXCVUKUYLUYOXDXEXFXGWSUWNUWEVU
        DUTVUMUWEVUDUWNUWBUWCUWMLXHXIXJXKXLXMXNXGXOXPXQUWFTUXIXRVUDTUXIXRXSUYSU
        XFUWMLUOZNUXIURZVUBVUEUWTUYOVUOVUBUTUWTUYOVUOVUBUWTUYOVUOUNZVUBUWTUWMUX
        KVTZVUBVUPUWTUXKVFVGVUQVUBVLKUXKVFUGXTUXKVFUWMYAYBUXJVUOCUWMHCUBUPUXHVU
        NNUXIUXGUWMUXFLYCYDYEYFYGXFWSVUDVUNTNUXIUWBUXFUWMLXHYHYIXMYJYKXOYLXLYMY
        KYNYOYPYQYRYQYSUWOUBHXRYTXFUYLUYAUWPUVAVOUVBUYAUAHUVCUWPUAHUVCXSYJUVDUV
        EUVFUVGUVHXGXFWSUVIUVJUVKUVLUVMUVNYNUVOUVP $.
        $([6-Apr-97]$)
    $}

  $}

  ${
    $v s $.
    $v r $.
    $v q $.
    $v a $.
    $v b $.
    $v c $.
    $v d $.
    $v j $.
    $v k $.
    $v m $.
    $v n $.
    zorn.vs $f set s $.
    zorn.vr $f set r $.
    zorn.vq $f set q $.
    zorn.va $f set a $.
    zorn.vb $f set b $.
    zorn.vc $f set c $.
    zorn.vd $f set d $.
    zorn.vj $f set j $.
    zorn.vk $f set k $.
    zorn.vm $f set m $.
    zorn.vn $f set n $.
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
      $( ` v u f g h t s r q a b c d j k m n ` are dummy. $)
    zorn.1 $e |- A e. V $.
    $( Zorn's Lemma of [Monk1] p. 117.  This theorem is equivalent to the
       Axiom of Choice and states that every partially ordered set ` A ` (with
       an ordering relation ` R ` ) in which every totally ordered subset has
       an upper bound, contains at least one maximal element.  The main proof
       consists of lemmas ~ zornlem1 through ~ zornlem7 ; this final piece
       mainly changes bound variables to eliminate the hypotheses of
       ~ zornlem7 . $)
    zorn   $p |- ( ( R Po A /\
      A. w ( ( w (_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( vt vu zorn.vr zorn.vq zorn.vn zorn.vj vg zorn.vb zorn.va zorn.vc
      zorn.vk zorn.vm vv vh zorn.vd zorn.vs vf cv con0 wcel wfn wel cfv cres
      wbr wn crn wral crab cuni wceq copab wi wal wa wex cab cima rdglem1
      cleqid weq breq2 biraldv breq1 cbvralv syl5bb cbvrabv id wb rneq raleq
      syl birabsdv eleq2d binegd bi2and biabdv eleq1 cbvabv syl5eq df-rab
      3eqtr4g unieqd cleqan12rd cbvopabv zornlem7 ) HIJKLMNEOUEZUFUGPUEZWNUHQOU
      IQUEZWOUJWOWPUKRUEZWQSUEZKUEZULZUMZRWSTUEZFULZKUAUEZUNZUOZTEUPZUOZSXGUPZU
      QZURZUARUSZUJURUTQVAVBVBOVCPVDZXCKUBUEZUNZUOZTEUPZUCUEZJUEZFULZUCXMUQZHUE
      VEUOJEUPZFUBUCUDYAXLXTUCYAIUEVEUOJEUPZDCABGOQUDNPUBXLVFYAVGXPXTUCXOUOZTJE
      TJVHZXRXBFULZUCXOUOYDXPYEYFXTUCXOXBXSXRFVIVJXCYFKUCXOWSXRXBFVKVLVMVNYBVGX
      KNUEZMUEZLUEZWSULZUMZMXQUOZLXQUPZUQZURUARUBNRNVHZUAUBVHZWQYGXJYNYOVOYPXIY
      MYPWRXGUGZXHVBZSVDZYIXQUGZYLVBZLVDZXIYMYPYIXGUGZWQYIWSULZUMZRXGUOZVBZLVDU
      VBYSYPUVGUVALYPUVCYTUVFYLYPXGXQYIYPXFXPTEYPXEXOURXFXPVPXDXNVQXCKXEXOVRVSV
      TZWAYPXGXQURZUVFYLVPUVHUVIYKMXGUOYLUVFYKMXGXQVRUVEYKRMXGRMVHUVDYJWQYHYIWS
      VKWBVLVMVSWCWDYRUVGSLSLVHZYQUVCXHUVFWRYIXGWEUVJXAUVERXGUVJWTUVDWRYIWQWSVI
      WBVJWCWFWGXHSXGWHYLLXQWHWIWJWKWLYCVGWM $.
      $([6-Apr-97]$)
  $}

