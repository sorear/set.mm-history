$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f g h $.  $( ` u g h ` are dummy. $)
    ac3a.1 $e |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $.
    $( Derivation of our AC from standard textbook AC.  Note that the
       Axiom of Regularity is required.  Equivalence is established
       using antepenultimate steps of proofs of ~ac2 and ~ac3 . $)
    ac3a $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu vg vh cv c0 wceq wn cfv wcel wi wral wex wel wa wrex wreu cpr cab
      wal hbra1 wmo ra4 fvex cleq3 biland birexdv bi2and cla4ev weq visset
      cleq1 binegd rcla4ev eqid mpan22 fveq2 preq1 syl preq2 eqtrd imlan
      r19.22i zfpaircl cleq2 abcl sylibr pri2 pri1 pm3.2i jctir cleq4 sylan5
      exp syl8 imp3a pm2.43d cleq3d bitrd bi2imd rcla4v wb prel12 ancom syl6bb
      sylan9rbb nel mtbii con2 adantarr pm5.32d preleq syl6bir cleq2d biimparc
      syl6 exp4c com13 com4r imp imp4a com3l r19.23aiv sylbi exp3a imp4b
      19.23adv df-rex syl5ib 19.21aiv mo2icl a1d jcad weu df-reu eu5 bitr
      syl6ibr 19.21ai df-ral copab cvv fvopab cleq1i birex biab fvrexex
      eqeltrr pm3.27 ssab2i ssexi rexeq bireudv bilimd biraldv 19.23aiv ax-mp
      ) CKZLMZNZUVDFKZOZUVDPZQZCAKZRZFSUVFCETZDETZUAZEBKZUBZDUVDUCZQZCUVKRZBSZU
      VLUWAFUVLUVFUVOEHKZLMZNZUWBUVGOZUWBUDZIKZMZUAZHUVKUBZIUEZUBZDUVDUCZQZCUVK
      RZUWAUVLCATZUWNQZCUFUWOUVLUWQCUVJCUVKUGUVLUWPUVFUWMUVLUWPUVFUAZDCTZUWLUAZ
      DSZUWTDUHZUAZUWMUVLUWRUXAUXBUVLUWRUXAUVLUWPUVFUWRUXAQZUVLUWPUVFUVIUXDUVJC
      UVKUIUVIUWRUXAUVIUVMUVHEKZPZUAZEUWKUBZUXAUWRUWTUVIUXHUADUVHUVDUVGUJZDKZUV
      HMZUWSUVIUWLUXHUXJUVHUVDUKUXKUVOUXGEUWKUXKUVNUXFUVMUXJUVHUXEUKULUMUNUOUWR
      UVHUVDUDZUWKPZUVDUXLPZUVHUXLPZUAZUAUXHUWRUXMUXPUWRUWDUWFUXLMZUAZHUVKUBZUX
      MUWRUWDHCUPZUAZHUVKUBZUXSUWPUVFCCUPZUYBUYAUVFUYCUAHUVDUVKCUQZUXTUWDUVFUXT
      UYCUXTUWCUVEUWBUVDLURUSUWBUVDUVDURUNUTCVAVBUYAUXRHUVKUXTUXQUWDUXTUWFUVHUW
      BUDZUXLUXTUWEUVHMUWFUYEMUWBUVDUVGVCUWEUVHUWBVDVEUWBUVDUVHVFVGVHVIVEUWJUXS
      IUXLUVHUVDVJZUWGUXLMZUWIUXRHUVKUYGUWHUXQUWDUWGUXLUWFVKULUMVLVMUXNUXOUVHUV
      DUYDVNUVHUVDUXIVOVPVQUXGUXPEUXLUWKUYFUXEUXLMUVMUXNUXFUXOUXEUXLUVDVRUXEUXL
      UVHVRUNUTVEVSVTWAWBWCUVLUXBUWRUVLUWTUXKQZDUFUXBUVLUYHDUVLUWSUWLUXKUVLUWSU
      WLUXKQUVLUWSUAZUXEUWKPZUVOUAZESUXKUWLUYIUYKUXKEUVLUWSUYJUVOUXKUYJUWSUVLUV
      OUXKQZUYJUWSUVLUYLUYJUWDUWFUXEMZUAZHUVKUBZUWSUVLUAZUYLQZUWJUYOIUXEEUQIEUP
      ZUWIUYNHUVKUYRUWHUYMUWDUWGUXEUWFVKULUMVLUYNUYQHUVKUYPHATZUYNUYLUYPUYSUWDU
      YMUYLUWSUVLUYSUWDUYMUYLQZQQUVLUYSUWDUWSUYTUVLUYSUWDUWEUWBPZUWSUYTQUVJUWDV
      UAQCUWBUVKHUQZCHUPZUVFUWDUVIVUAVUCUVEUWCUVDUWBLURUSVUCUVIUWEUVDPVUAVUCUVH
      UWEUVDUVDUWBUVGVCZWDUVDUWBUWEVRWEWFWGUYMUWSVUAUYLUYMUWSVUAUVOUXKUYMUWSVUA
      UAZUVOUAZUXJUWEMZVUCUAZUXKUYMVUFVUEUXJUVDUDUWFMZUAVUHUYMVUEVUIUVOUYMVUEVU
      IUVOWHZUYMUWSVUJVUAUYMDCUPZNZVUJUWSVULVUIUXJUWFPZUVDUWFPZUAZUYMUVOUXJUVDU
      WEUWBDUQZUYDUWBUVGUJZVUBWIUYMVUOUVNUVMUAUVOUYMVUMUVNVUNUVMUWFUXEUXJVRUWFU
      XEUVDVRUNUVNUVMWJWKWLVUKUWSVUKDDTUWSUXJUVDUXJVRDWMWNWOVSWPVTWQUXJUVDUWEUW
      BVUPUYDVUQVUBWRWSVUCUXKVUGVUCUVHUWEUXJVUDWTXAXBXCXDWAXEXFXGXHXIXJXKXDXLXM
      UVOEUWKXNXOVTWBXPUWTDUVHXQVEXRXSUWMUWTDXTUXCUWLDUVDYAUWTDYBYCYDXKYEUWNCUV
      KYFVMUVTUWOBUWKUWKUWHHUVKUBZIUEZUWBJKUXEUVGOZUXEUDZMEJYGZOZUWGMZHUVKUBZIU
      EVUSYHVVEVURIVVDUWHHUVKVVCUWFUWGEJUWBVVAUWFVUBUWEUWBVJEHUPZVVAUWEUXEUDZUW
      FVVFVUTUWEMVVAVVGMUXEUWBUVGVCVUTUWEUXEVDVEUXEUWBUWEVFVGYIYJYKYLHIUVKVVBAU
      QYMYNUWJVURIUWIUWHHUVKUWDUWHYOVIYPYQUVPUWKMZUVSUWNCUVKVVHUVRUWMUVFVVHUVQU
      WLDUVDUVOEUVPUWKYRYSYTUVAUOVEUVBGUVC $.
      $([17-Oct-96]$)
  $}

  ${
    $d x y z w v u $.
    $( Axiom of Choice using abbreviations.  Note:  The antepenultimate step
       of the proof shows the logical equivalence.  $)
    ac2 $p |- E. y A. z e. x A. w e. z E! v e. z E. u e. y
              ( z e. u /\ v e. u ) $=
      ( wel wa wex weq wb wal wi cv wrex wreu wral 19.21v impexp bi2.04 bitr
      bial weu df-eu df-reu 19.42v an41r3 anass bitr3 biex df-rex cleq3 cleq4
      bi2and cbvexv bilan 3bitr4 birbi bilim df-ral ax-17 hba1 hbex hbim
      birimd cbv4 19.5 ax-ac mpbi ) ECGZCAGZHZVLEAGZABGZHHZAIZEAJZKZELZAIZMZCL
      ELZBICFGZEFGZHZFBNZOZECNZPZDWHQZCANZQZBIWBWLBWAELZCLVKWJMZCLWBWLWMWNCVKVJ
      VTMZMZELVKWOELZMWMWNVKWOERWAWPEWAVJVKVTMMWPVJVKVTSVJVKVTTUAUBWJWQVKDCGZWI
      MZDLWRVTMZDLWJWQWSWTDWIVTWRVJWGHZEUCXAVQKZELZAIWIVTXAEAUDWGEWHUEVSXCAVRXB
      EVPXAVQVJVNVKVMHZHZHZAIVJXEAIZHVPXAVJXEAUFVOXFAVOVJVNHXDHXFVJVNVKVMUGVJVN
      XDUHUIUJWGXGVJWGFBGZWEHZFIXGWEFWFUKXIXEFAFAJZXHVNWEXDFNZWKWFULXJWCVKWDVMX
      KWKWHUMXKWKENZUMUNUNUOUAUPUQURUBUJUQUSUBWIDWHUTWOWTEDWODVAWRVTEWREVAVSEAV
      REVBVCVDEDJVJWRVTXLDNWHULVEVFUQUSUQUBWAECVGWJCWKUTUQUJBECAVHVI $.
      $([18-Jul-96]$)
  $}

  ${
    $d x y z w v u $.  $( ` u ` is dummy. $)
    $( Axiom of Choice using abbreviations.  Note:  The antepenultimate step
       of the proof shows the logical equivalence. $)
    ac3 $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu wel wa cv wrex wreu wral wex c0 wceq wn wi wal ax-17 19.23 df-ral
      nnull birim 3bitr4 biral biex ac2 mpbi ) CEGDEGHEBIJDCIZKZFUILZCAIZLZBMUI
      NOPZUJQZCULLZBMUMUPBUKUOCULFCGZUJQFRUQFMZUJQUKUOUQUJFUJFSTUJFUIUAUNURUJFU
      IUBUCUDUEUFABCFDEUGUH $.
      $([19-Jul-96]$)
  $}

  ${
    $d x z f y w v u g $.  $( ` y w v u g ` are dummy. $)
    $( Equivalent of Axiom of Choice.  Note that ` f ` is not necessarily a
       function.  Axiom of Choice, Takeuti and Zaring, p. 83. $)
    ac4 $p |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $=
      ( vv vw vy vu vg cv c0 wceq wn wel wa wrex wreu wi wral wex cfv wcel
      crab cuni copab wal visset zfausrab zfun2 weq cab cleq4 cleq3 birand
      birexdv bi2and biabdv df-rab 3eqtr4g unieqd cleqid opabval2 cleq3d
      reucl syl5bir syl3d a2i 19.20i df-ral 3imtr4 wmo moeq a1i funopabex
      fveq1 bilimd biraldv cla4ev syl 19.23aiv ac3 ax-mp ) BIZJKLZBDMZEDMZNZD
      FIZOZEWBPZQZBAIZRZFSWCWBCIZTZWBUAZQZBWKRZCSZWLWRFWLWCWBGAMZHIGDMZWENZDWGO
      ZEGIZUBZUCZKZNGHUDZTZWBUAZQZBWKRZWRBAMZWJQZBUEXLXJQZBUEWLXKXMXNBXLWJXJXLW
      IXIWCXLXIWHEWBUBZUCZWBUAWIXLXHXPWBGHWBXEXPWKXGXOWHEWBBUFUGUHGBUIZXDXOXQEG
      MZXBNZEUJEBMZWHNZEUJXDXOXQXSYAEXQXRXTXBWHXCWBEIUKXQXAWFDWGXQWTWDWEXCWBDIU
      LUMUNUOUPXBEXCUQWHEWBUQURUSXGUTVAVBWHEWBVCVDVEVFVGWJBWKVHXJBWKVHVIWQXKCXG
      XFGHWKAUFXFHVJWSHXEVKVLVMWMXGKZWPXJBWKYBWOXIWCYBWNXHWBWBWMXGVNVBVOVPVQVRV
      SAFBEDVTWA $.
      $([21-Jul-96]$)
  $}

  ${
    $d x y f A $.  $( ` y ` is dummy. $)
    ac4c.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice (class version) $)
    ac4c $p |- E. f A. x e. A ( -. x = (/) -> ( f ` x ) e. x ) $=
      ( vy cv c0 wceq wn cfv wcel wi wral wex raleq biexdv ac4 vtoclbv ) AFZGHI
      SCFJSKLZAEFZMZCNTABMZCNEBDUABHUBUCCTAUABOPEACQR $.
      $([10-Feb-97]$)
  $}

  ${
    $d f x z y w g $.  $( ` y w g ` are dummy. $)
    $( Equivalent of Axiom of Choice. $)
    ac5 $p |- E. f ( f Fn x /\ A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) ) $=
      ( vg vw vy cv c0 wceq wn cfv wcel wi wral wex wfn wa wel copab wal fvex
      fveq2 cleqid opabval2 cleq3d bilimd pm5.74i bial df-ral 3bitr4 bilan
      weu eueq1 a1i fnopab mpbiran visset wmo moeq funopabex fneq1 fveq1
      biraldv bi2and cla4ev sylbir 19.23aiv ac4 ax-mp ) BGZHIJZVJDGZKZVJLZMZB
      AGZNZDOCGZVPPZVKVJVRKZVJLZMZBVPNZQZCOZVQWEDVQEARZFGEGZVLKZIZQEFSZVPPZVKVJ
      WJKZVJLZMZBVPNZQZWEWPWKVQWOVQWKBARZWNMZBTWQVOMZBTWOVQWRWSBWQWNVOWQWMVNVKW
      QWLVMVJEFVJWHVMVPWJVJVLUAWGVJVLUBWJUCZUDUEUFUGUHWNBVPUIVOBVPUIUJUKWIEFVPW
      JWIFULWFFWHWGVLUAUMUNWTUOUPWDWPCWJWIEFVPAUQWIFURWFFWHUSUNUTVRWJIZVSWKWCWO
      VPVRWJVAXAWBWNBVPXAWAWMVKXAVTWLVJVJVRWJVBUEUFVCVDVEVFVGABDVHVI $.
      $([21-Jul-96]$)
  $}

  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x ( x e. On /\ ( f Fn x /\
                A. y ( y e. x -> ( f ` y ) = ( G ` ( f |` y ) ) ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` P~ A `. $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for Theorem 10.3 of Takeuti and Zaring, p. 84. $)
    numthlem   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 wceq wn cfv wcel wi cpw wral wex wf1o con wrex cima cdif cres wa
      tfr2 crn copab cvv wfun resfunexg visset wfn fnfun tfr1 ax-mp mp2 fvex
      rneq difeq2 fveq2 3syl fvopab fveq1 df-ima 3eqtr4 syl6eq cleq3d difss
      ssexi cleq1 binegd id cleq34d bi2imd rcla4v wss sselpw mpbi mpi imp
      syl5bir exp3a r19.21aiv tz7.49c f1oeq1 cla4ev r19.22i 19.23aiv zfpowcl
      ac4c ) BMZNOZPZWPFMZQZWPRZSZBCTZUAZFUBAMZCEMZUCZEUBZAUDUEZXDXIFXDCGXEUFZU
      GZNOZPZXEGQZXKRZSZAUDUAXECGXEUHZUCZAUDUEXIXDXPAUDXEUDRZXDXMXOXSXOXKWSQZXK
      RZXDXMUIXSXNXTXKXSXNXQHQZXTABADEGHJKUJXQWPCXFUKZUGZWSQZOEBULZQZCXQUKZUGZW
      SQZYBXTEBXQYEYJXEUMRGUNZXQUMRGXEUMUOAUPGUDUQYKUDGURABDEGHJKUSZUTVAZYIWSVB
      XFXQOYCYHOYDYIOYEYJOXFXQVCYCYHCVDYDYIWSVEVFVGHYFOYBYGOXQHYFVHLUTXKYIOZXTY
      JOXKYIWSVEXJYHOYNXJYHCVDGXEVIUTUTVJVKVLXDXMYAXDXKXCRZXMYASZXBYPBXKXCXKCIC
      XJVMZVNZWPXKOZWRXMXAYAYSWQXLWPXKNVOVPYSWTXTWPXKWPXKWSVEYSVQVRVSVTXKCWAYOX
      KCYRWBYQWCWDWEWFWGWHACGYLIWIXRXHAUDXGXREXQYMXECXFXQWJWKWLVFWMBXCFCIWNWOUT
      $.
      $([10-Feb-97]$)
  $}

  ${
    $d x y z w v u f g h A $.  $( ` y z w v u g h ` are dummy. $)
    numth.1 $e |- A e. V $.
    $( Numeration theorem:  any set can be put into 1-1 correspondence with some
       ordinal (using AC).  Theorem 10.3 of Takeuti and Zaring, p. 84. $)
    numth   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vz vg vw vu vv vh cv con wcel wfn wel cfv cres crn cdif wceq copab
      wi wal wa wex cab cuni rfglem1 cleqid weq id rneq difeq2 fveq2 3syl
      cleqan12rd cbvopabv numthlem ) AEBFLZMNGLZUTOHFPHLZVAQVAVBRILZBJLZSZTZKLZQ
      ZUAZJIUBZQUAUCHUDUEUEFUFGUGZCKVKUHZVJDFHAEGCVJUIVLUJVIELZBCLZSZTZVGQZUAJI
      CEIEUKZJCUKZVCVMVHVQVRULVSVEVOUAVFVPUAVHVQUAVDVNUMVEVOBUNVFVPVGUOUPUQURUS
      $.
      $([10-Feb-97]$)
  $}

  ${
    $d x y z w f A $.  $( ` y z w f ` are dummy. $)
    weth.1 $e |- A e. V $.
    $( Well-ordering theorem: any set can be well-ordered (using AC).
       Theorem 6 of Suppes, p. 242. $)
    weth   $p |- E. x x We A $=
      ( vy vf vz vw cv wf1o wex con wrex wwe wcel wi ccnv f1ocnv cep cfv wbr
      copab cleqid f1owe cxp cin weinxp xpex zfauscl2 weeq1 cla4ev sylbi syl6
      word eloni ordwe syl syl5 19.23aiv comm r19.23aiv numth ax-mp ) DHZBEHZIZ
      EJZDKLBAHZMZAJZVFVIDKVFVCKNZVIVEVJVIOZEVEBVCVDPZIZVKVCBVDQVMVCRMZVIVJVMVN
      BFHVLSGHVLSRTFGUAZMZVIFGBVCVORVLVOUBUCVPBVOBBUDZUEZMZVIBVOUFVHVSAVRVQVOBB
      CCUGUHBVGVRUIUJUKULVJVCUMVNVCUNVCUOUPUQUPURUSUTDBECVAVB $.
      $([9-Mar-97]$)
  $}


  ${
    $v t $.
    $v s $.
    $v r $.
    $v a $.
    $v b $.
    zornlem.vt $f var t $.
    zornlem.vs $f var s $.
    zornlem.vr $f var r $.
    zornlem.va $f var a $.
    zornlem.vb $f var b $.
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.
    $d h t f G $.
    $d t C $.  $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zornlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    zornlem.2 $e |- B = { f | E. h ( h e. On /\ ( f Fn h /\
                A. t ( t e. h -> ( f ` t ) = ( G ` ( f |` t ) ) ) ) ) } $.
    zornlem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zornlem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zornlem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A `. $)
    zornlem.6 $e |- G =
       { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.

    $( Lemma for Zorn's lemma. $)
    zornlem1 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con wcel wwe c0 wceq wn wa cfv wbr wral crab cuni cres tfr2 copab
      fveq1 ax-mp cvv wfun resfunexg visset wfn fnfun tfr1 mp2 wss cima sseq1i
      ssrab mpbir ssexi zfausrab zfun2 cab crn rneq df-ima syl6eqr cleq4d
      birimd biraldv2 rbiabdv 3eqtr4g bi2and biabdv df-rab unieqd fvopab eqtr
      syl6eq cleq3d wreu wereu mpan21 reucl syl syl5bir imp ) AUCZUDUEZFCUCZUFZ
      IUGUHUIZUJZXBNUKZIUEZXCXIEUCZDUCZXDULUIZEIUMZDIUNZUOZIUEZXGXCXHXOIXCXHNXB
      UPZOUKZXOMPAGKNORSUQXRXQPUCXLEHUMZDHUNZUOZUHKPURZUKZXOOYBUHXRYCUHXQOYBUSU
      BUTKPXQYAXOXBVAUENVBZXQVAUENXBVAVCAVDNUDVEYDUDNVFMPGKNORSVGUTVHXNXMDIIFQI
      FVIZLUCZBUCJULZLNXBVJZUMZBFUNZFVIIYJFUAVKYIBFVLVMZVNZVOVPKUCZXQUHZXTXNYNX
      KHUEZXSUJZDVQXKIUEZXMUJZDVQXTXNYNYPYRDYNYOYQXSXMYNHIXKYNYGLYMVRZUMZBFUNYJ
      HIYNYTYIBFYNYGYGLYSYHYNYFYSUEYFYHUEYGYNYSYHYFYNYSXQVRYHYMXQVSNXBVTWAWBWCW
      DWETUAWFZWBYNXLXLEHIYNXJHUEXJIUEXLYNHIXJUVAWBWCWDWGWHXSDHWIXMDIWIWFWJWKWL
      WMWNXGXMDIWOZXPXEYEXFUVBDEFIXDYLWPYKWQXMDIWRWSWTXA $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem2 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con wcel wwe c0 wceq wn wa wel cfv cima wbr wi cdm wss onsst wfn
      fndm tfr1 ax-mp sseq2i sylibr wfun funfvima fnfun mpan syl adantr
      zornlem1 wral crab cleq4i fvex breq2 biraldv rabcl bitr pm3.27 sylbi
      breq1 rcla4v syld ) AUDZUEUFZGDUDUGJUHUIUJUKZUKZBAULZBUDZOUMZOWFUNZUFZWLW
      FOUMZKUOZWGWJWNUPZWHWGWFOUQZURZWQWGWFUEURWSWFUSWRUEWFOUEUTZWRUEUIUEOVANQH
      LOPSTVBZVCVDVEOVFZWSWQWFWKOVGWTXBUEOVHXAVCVIVJVKWIWOJUFZWNWPUPZACDEFGHIJK
      LMNOPQRSTUAUBUCVLXCMUDZWOKUOZMWMVMZXDXCWOGUFZXGUKZXGXCWOXECUDZKUOZMWMVMZC
      GVNZUFXIJXMWOUBVOXLXGCWOGWFOVPXJWOUIXKXFMWMXJWOXEKVQVRVSVTXHXGWAWBXFWPMWL
      WMWKOVPXEWLWOKWCWDVJVJWE $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ -. D = (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con wcel wwe c0 wceq wn wa wel cfv wbr wi zornlem2 adantl breq1
      biimprcd ponr nsyli comm zornlem1 cima wral crab ssrab eqsstr ssi syl
      sylan5 syld ) GKUDZAUEZUFUGGDUEUHJUIUJUKULULZULBAUMZBUEOUNZVOOUNZKUOZVSVR
      UJZUKZVPVQVTUPVNABCDEFGHIJKLMNOPQRSTUAUBUCUQURVNVSGUGZVTWBUPVPVTVNWCULZWB
      VTWAVSVSKUOZWDWAWEVTVSVRVSKUSUTGVSKVAVBVCVPVSJUGWCACDEFGHIJKLMNOPQRSTUAUB
      UCVDJGVSJMUECUEKUOMOVOVEVFZCGVGGUBWFCGVHVIVJVKVLVM $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa con wcel c0 wceq wex wrex crn cvv wn wi wal wss cfv
      ax-17 hba1 hban cleq3 zornlem1 wbr cima wral crab ssrab eqsstr ssi syl
      syl5bi comm exp32 a2d imp4a a4sd imp 19.23ad wfn wb fvelrn tfr1 ax-mp
      syl5ib ssrdv ssex exp adantl ccnv wfun wel zornlem3 exp45 com23 19.21adv
      19.21aiiv r2al syl6ibr cres tz7.48lem ssid mpan funeq cnveq wrel cdm
      relssres fnrel fndm sseq1i mpbir mp2an sylibr syl6 funrnex df-rn cleq3i
      dfdm4 eqtr3 3imtr4g tz7.13 mtoi jcad alnex syl5ibr pm3.24 mt3i df-rex )
      FJUDZFCUEUFZUGZAUEZUHUIZIUJUKZUGAULZYQAUHUMYNYRNUNZUOUIZYTUPZUGZYNYPYQUPZ
      UQZAURZUVBYRUPYNUVEYTUVAYMUVEYTUQYLYMUVEYTYMUVEUGZYSFUSYTUVFUCYSFUVFYPYON
      UTZUCUEZUKZUGZAULZUVHFUIZUVHYSUIZUVFUVJUVLAYMUVEAYMAVAUVDAVBVCUVLAVAYMUVE
      UVJUVLUQZYMUVDUVNAYMUVDYPUVIUVLYMYPUVCUVIUVLUQZYPYMUVCUVOUQYPYMUVCUVOUVIY
      PYMUVCUGUGZUVLUVIUVGFUIZUVLUVPUVGUVHFVDUVPUVGIUIUVQABCDEFGHIJKLMNOPQRSTUA
      UBVEIFUVGILUEBUEJVFLNYOVGVHZBFVIFUAUVRBFVJVKVLVMVNVOVPVOVQVRVSVTWANUHWBZU
      VMUVKWCAUHUVHNWDMPGKNORSWEZWFWGWHYSFQWIVMWJWKYNUVENWLZWMZUVAYNUVEUVGUVHNU
      TUKUPZUCYOVHAUHVHZUWBYNUVEYPUCAWNZUGUWCUQZUCURZAURUWDYNUVDUWGAYNUVDUWFUCY
      NUVDYPUWEUWCYNYPUVCUWEUWCUQZYLYMYPUVCUWHUQZUQYLYPYMUWIYLYPYMUVCUWHAUCBCDE
      FGHIJKLMNOPQRSTUAUBWOWPWQVTVQVRWRWSUWCAUCUHYOWTXAUWDNUHXBZWLZWMZUWBUHUHUS
      ZUWDUWLAUCUHNUVTXCUHXDZXEUWAUWKUKZUWBUWLWCUWAUWKXFNUWJUKZUWONUWJXGNXHZNXI
      ZUHUSZUWPNUHXJUVSUWQUHNXKUVTWFUWSUWMUWRUHUHUVSUWRUHUKUHNXLUVTWFZXMUWNXNXO
      WFWFXPXQUWBYTUHUOUIZUWBUWAXIZUOUIZUWAUNZUOUIZYTUXAUXCUWBUXEUOUWAXRVOYSUXB
      UONXSXTUHUXDUOUWRUHUXDUWTNYAYBXTYCYDYEXQYFYPYQAYGYHYTYIYJYQAUHYKXP $.
      $([3-Apr-97]$)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y `. $)
      zornlem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for Zorn's lemma. $)
      zornlem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) ->
            ( F " x ) (_ A ) $=
      ( zornlem.vs cv wwe con wcel wa c0 wceq wn wral cima wel cfv wex ax-17
      hbra1 hban wi wal cleq3 zornlem1 wbr crab ssrab eqsstr ssi syl syl5bi
      ontrt syland comm exp43 com13 imp a2d a4sd df-ral syl5ib imp4b 19.23ad
      wfun fvelima wfn fnfun tfr1 ax-mp mpan syl5 ssrdv ) GDUGUHZAUGZUIUJZUKZQU
      LUMUNZBWPUOZUKZUFOWPUPZGXABAUQZBUGZOURZUFUGZUMZUKZBUSZXFGUJZXFXBUJZXAXHXJ
      BWRWTBWRBUTWSBWPVAVBXJBUTWRWTXCXGXJWRXCWSVCZBVDXCXGXJVCZVCZWTWRXLXNBWRXCW
      SXMWOWQXCWSXMVCZVCXCWQWOXOXCWQWOWSXMXGXCWQUKZWOWSUKZUKXJXGXDUIUJZXQXJXPXG
      XEGUJZXJXRXQUKZXEXFGVEXTXEQUJXSBCDEFGHIQKLMNOPRSTUAUBUEUDVFQGXEQMUGCUGKVG
      MOXDUPUOZCGVHGUEYACGVIVJVKVLVMXDWPVNVOVPVQVRVSVTWAWSBWPWBWCWDWEOWFZXKXIBX
      FWPOWGOUIWHYBUIOWINRHLOPTUAWJWKWLWMWN $.
      $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) -> R Or ( F " x ) ) ) $=
      ( zornlem.vr zornlem.vs zornlem.va zornlem.vb wpo cv wwe con wcel wa c0
      wceq wn wral cima wbr weq w3o wor wss wi zornlem5 poss syl comm wal wel
      cfv wex crab anandir ontrt im2an sylbi ancoms exp cleqid zornlem2
      adantall breq12 biimpcd syl6 com23 pm3.27 imlan sylan5 imp cleq12 fveq2
      syl5bi adantl adantalr wb pm3.26 im3ord word eloni tz7.10 syl5 exp31
      com4r pm2.43i exp4a com3r a2d cleq1i bineg biral visset imaeq2 raleq
      rbiabdv cleq1d binegd rcla4v im2and imp4a 19.23advv wfun fvelima wfn
      fnfun tfr1 ax-mp an4 bi2ex eeanv bitr sylibr 19.21aivv r2al a1i jcad
      df-so syl6ibr ) GKUJZGDUKULZAUKZUMUNZUOZQUPUQZURZBUVCUSZUOZOUVCUTZKUJZUFU
      KZUGUKZKVAZUFUGVBZUVMUVLKVAZVCZUGUVJUSUFUVJUSZUOUVJKVDUVAUVIUVKUVRUVIUVAU
      VKUVIUVJGVEUVAUVKVFABCDEFGHIJKLMNOPQRSTUAUBUCUDUEVGUVJGKVHVIVJUVIUVRVFUVA
      UVIUVLUVJUNZUVMUVJUNZUOZUVQVFZUGVKUFVKUVRUVIUWBUFUGUVIUHAVLZUIAVLZUOZUHUK
      ZOVMZUVLUQZUIUKZOVMZUVMUQZUOZUOZUIVNUHVNZUVQUWAUVIUWMUVQUHUIUVEUVHUWMUVQV
      FUVEUVHUWEUWLUVQUVEUWEMUKCUKKVAZMOUWFUTZUSZCGVOZUPUQZURZUWOMOUWIUTZUSZCGV
      OZUPUQZURZUOZVFZUWEUWLUVQVFZVFUVHUVEUWEUXFUXHUVBUVDUWEUXFUXHVFZVFUVDUWEUV
      BUXIUVDUWEUVBUXFUXHUVDUWEUWFUMUNZUWIUMUNZUOZUVBUXFUOZUXHVFZUVDUWEUXLUWEUV
      DUXLUWEUVDUOUWCUVDUOZUWDUVDUOZUOUXLUWCUWDUVDVPUXOUXJUXPUXKUWFUVCVQUWIUVCV
      QVRVSVTWAUXLUXNUXLUXMUWLUXLUVQUXLUXMUWLUXLUVQVFUXLUXMUOZUWLUOZUHUIVLZUHUI
      VBZUIUHVLZVCZUVQUXLUXRUXSUVNUXTUVOUYAUVPUXQUWLUXSUVNVFZUXLUVBUXEUOZUWLUYC
      VFUXMUXLUYDUOZUXSUWLUVNUYEUXSUWGUWJKVAZUWLUVNVFUXKUYDUXSUYFVFUXJUIUHCDEFG
      HIUXCKLMNOPRSTUAUBUXCWBUDWCWDUWLUYFUVNUWGUVLUWJUVMKWEWFWGWHUXFUXEUVBUWTUX
      EWIWJWKWLUWLUXTUVOVFUXQUWLUWGUWJUQUVOUXTUWGUVLUWJUVMWMUWFUWIOWNWOWPUXQUWL
      UYAUVPVFZUXLUVBUWTUOZUWLUYGVFUXMUXLUYHUOZUYAUWLUVPUYIUYAUWJUWGKVAZUWLUVPV
      FUXJUYHUYAUYJVFUXKUHUICDEFGHIUWRKLMNOPRSTUAUBUWRWBUDWCWQUWLUYJUVPUWKUWHUY
      JUVPWRUWJUVMUWGUVLKWEVTWFWGWHUXFUWTUVBUWTUXEWSWJWKWLWTUXLUWFXAZUWIXAZUOUY
      BUXJUYKUXKUYLUWFXBUWIXBVRUWFUWIXCVIXDXEXFXGWGXHXIWLXJUVHUWOMOBUKZUTZUSZCG
      VOZUPUQZURZBUVCUSZUXGUVGUYRBUVCUVFUYQQUYPUPUEXKXLXMUYSUWCUWTUWDUXEUYRUWTB
      UWFUVCUHXNBUHVBZUYQUWSUYTUYPUWRUPUYTUYOUWQCGUYTUYNUWPUQUYOUWQWRUYMUWFOXOU
      WOMUYNUWPXPVIXQXRXSXTUYRUXEBUWIUVCUIXNBUIVBZUYQUXDVUAUYPUXCUPVUAUYOUXBCGV
      UAUYNUXAUQUYOUXBWRUYMUWIOXOUWOMUYNUXAXPVIXQXRXSXTYAVSXDYBWLYCUWAUWCUWHUOZ
      UHVNZUWDUWKUOZUIVNZUOZUWNOYDZUWAVUFVFVUGUVSVUCUVTVUEVUGUVSVUCUHUVLUVCOYEW
      AVUGUVTVUEUIUVMUVCOYEWAYAOUMYFVUGUMOYGNRHLOPTUAYHYIYIUWNVUBVUDUOZUIVNUHVN
      VUFUWMVUHUHUIUWCUWDUWHUWKYJYKVUBVUDUHUIYLYMYNXDYOUVQUFUGUVJUVJYPYNYQYRUFU
      GUVJKYSYT $.
      $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem7 $p |- ( ( R Po A /\
      A. s ( ( s (_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
      ( wpo cv wss wor wa wbr weq wo wral wrex wi wal wwe wex wn c0 wceq con
      zornlem4 cima crab wb imaeq2 raleq syl rbiabdv 3eqtr4g cleq1d onminex
      wcel zornlem5 ssd w3a potr 3anass sylan5br exp3a com23 imp breq1
      biimprcd adantl jaod exp42 sylan9r com24 imp31 a2d 19.21aiiv df-ral
      3imtr4g cleq1i cleq4 sylbi visset breq2 biraldv rabcl syl5bbr biimpd
      rcbvalv syl5ib exp32 com34 noel mtoi exp4a exp com4r pm2.43a imp3a com4l
      19.21adv syl6ibr imdistan 19.22dv df-rex comm adantr imp32 a1i zornlem6
      jcad cvv wfun zfrep5 wfn fnfun tfr1 ax-mp mp2 sseq1 soeq2 bi2and birexdv
      bi2imd cla4v sylan9 adantld mpd exp45 imp4a com3l r19.23aiv 3syl
      adantalr pm2.43i 19.23adv weth mpi ) GKUJZSUKZGULZUWAKUMZUNZTUKZUAUKZKUOZ
      TUAUPZUQZTUWAURZUAGUSZUTZSVAZUNZGDUKVBZDVCUWFUBUKZKUOZVDZUBGURZUAGUSZUWNU
      WOUWTDUWNUWOUWTUWNUWOUNZUWTUVTUWOUXAUWTUTZUWMUVTUWOUNJVEVFZAVGUSUXCQVEVFZ
      VDBAUKZURZUNZAVGUSUXBACDEFGHIJKLMNOPRUCUDUEUFUGUHVHUXCUXDABABUPZJQVEUXHMU
      KZCUKZKUOZMOUXEVIZURZCGVJZUXKMOBUKZVIZURZCGVJJQUXHUXMUXQCGUXHUXLUXPVFUXMU
      XQVKUXEUXOOVLUXKMUXLUXPVMVNVOUGUIVPVQVRUXGUXBAVGUXAUXEVGVSZUXGUWTUXAUXRUX
      CUXFUWTUWNUWOUXRUXCUXFUWTUTZUTZUTUWNUWOUXRUXTUWNUXCUWOUXRUNZUXSUWNUXCUYAU
      XFUWTUWNUXCUYAUXFUNZUNZUNUWITUXLURZUAGUSZUWTUWNUXCUYBUYEUWTUTZUVTUXCUYBUY
      FUTZUTUWMUXCUVTUYGUXCUVTUYBUYFUXCUVTUYBUNZUNZUWFGVSZUYDUNZUAVCUYJUWSUNZUA
      VCUYEUWTUYIUYKUYLUAUYIUYJUYDUWSUTUTUYKUYLUTUYIUYJUYDUWSUYIUYKUWPGVSZUWRUT
      ZUBVAUWSUYIUYKUYNUBUYIUYJUYDUYNUYMUYIUYJUYDUWRUYMUXCUYHUYJUYDUWRUTZUTZUXC
      UYMUYHUYPUTUXCUYMUYHUYMUYPUXCUYMUYHUYMUYPUTUTUXCUYMUNZUYHUYJUYMUYOUYQUYHU
      YJUYMUYOUYQUYHUYJUYMUNZUYDUWRUYQUYHUYRUNZUNUYDUNUWQUWPVEVSZUYQUYSUYDUWQUY
      TUTUYQUYSUWQUYDUYTUYQUYSUWQUYDUYTUTUYSUWQUNZUYDUWEUWPKUOZTUXLURZUYQUYTVUA
      UWEUXLVSZUWIUTZTVAVUDVUBUTZTVAUYDVUCVUAVUEVUFTVUAVUDUWIVUBUYHUYRUWQVUDUWI
      VUBUTZUTZUYHUWQUYRVUHUYHVUDUYRUWQVUGUYBVUDUWEGVSZUVTUYRUWQVUGUTUTUYBUXLGU
      WEABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIVTZWAUVTVUIUYRUWQVUGUVTVUIUYRUNZUNZUWQU
      NUWGVUBUWHVULUWQUWGVUBUTVULUWGUWQVUBVULUWGUWQVUBUVTVUIUYJUYMWBUWGUWQUNVUB
      UTVUKGUWEUWFUWPKWCVUIUYJUYMWDWEWFWGWHUWQUWHVUBUTVULUWHVUBUWQUWEUWFUWPKWIW
      JWKWLWMWNWOWGWPWQWRUWITUXLWSVUBTUXLWSWTUYQUXIUWPKUOZMUXLURZUYTVUCUXCUYMVU
      NUYTUTUXCUYMVUNUYTUXCUYMVUNUNZUYTUXCUWPUXNVSZUYTVUOUXCUXNVEVFVUPUYTVKJUXN
      VEUGXAUXNVEUWPXBXCUXMVUNCUWPGUBXDCUBUPUXKVUMMUXLUXJUWPUXIKXEXFXGXHXIWFWHV
      UBVUMTMUXLUWEUXIUWPKWIXJXKWNXLXMWPUWPXNXOWMXPXMXQXRXSXTYAXTYBUWRUBGWSYCWF
      UYJUYDUWSYDVNYEUYDUAGYFUWSUAGYFWTXLYGYHYIUWNUYCUYEUWNUYBUYEUXCUVTUYBUXLGU
      LZUXLKUMZUNZUWMUYEUVTUYBVUQVURUYBVUQUTUVTVUJYJABCDEFGHIJKLMNOPQRUCUDUEUFU
      GUHUIYKYLUWLVUSUYEUTSUXLUXEYMVSOYNZUXLYMVSOUXEYMYOAXDOVGYPVUTVGOYQNRHLOPU
      DUEYRYSYTUWAUXLVFZUWDVUSUWKUYEVVAUWBVUQUWCVURUWAUXLGUVAUWAUXLKUVBUVCVVAUW
      JUYDUAGUWITUWAUXLVMUVDUVEUVFUVGUVHWHUVIUVJWGWFWHUVKUVLUVMUVNUVOUVPXQUVQDG
      UCUVRUVS $.
      $([6-Apr-97]$)
    $}

  $}

  ${
    $v t $.
    $v s $.
    $v r $.
    $v q $.
    $v a $.
    $v b $.
    $v c $.
    $v d $.
    $v j $.
    $v k $.
    $v m $.
    $v n $.
    zorn.vt $f var t $.
    zorn.vs $f var s $.
    zorn.vr $f var r $.
    zorn.vq $f var q $.
    zorn.va $f var a $.
    zorn.vb $f var b $.
    zorn.vc $f var c $.
    zorn.vd $f var d $.
    zorn.vj $f var j $.
    zorn.vk $f var k $.
    zorn.vm $f var m $.
    zorn.vn $f var n $.
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
      $( ` v u f g h t s r q a b c d j k m n ` are dummy. $)
    zorn.1 $e |- A e. V $.
    $( Zorn's Lemma, Monk1, p. 117.  Requires Axiom of Choice.  The main
       proof is in ~zornlem7 ; this proof mainly changes bound variables
       to eliminate the hypotheses of ~zornlem7 .$)
    zorn   $p |- ( ( R Po A /\
      A. w ( ( w (_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( zorn.vt vu zorn.vr zorn.vq zorn.vn zorn.vj zorn.vb zorn.va zorn.vc
      zorn.vk zorn.vm vv vh zorn.vd zorn.vs vf vg cv con wcel wfn wel cfv cres
      wbr wn crn wral crab cuni wceq copab wi wal wa wex cab cima rfglem1
      cleqid ax-17 weq cleq3 breq2 biraldv breq1 rcbvalv syl5bb bi2and cbvab
      df-rab 3eqtr4 id wb rneq raleq syl rbiabdv cleq4d binegd biabdv syl5eq
      3eqtr4g unieqd cleqan12rd cbvopabv zornlem7 ) HIJKLMENUEZUFUGOUEZWOUHPNUI
      PUEZWPUJWPWQUKQUEZWRRUEZKUEZULZUMZQWTSUEZFULZKTUEZUNZUOZSEUPZUOZRXHUPZUQZ
      URZTQUSZUJURUTPVAVBVBNVCOVDZXDKUAUEZUNZUOZSEUPZUBUEZJUEZFULZUBXNUQZHUEVEU
      OJEUPZFUAUBUCYBXMYAUBYBIUEVEUOJEUPZUDDCABGNPUCUDOUAXMVFYBVGXCEUGZXQVBZSVD
      XTEUGZYAUBXPUOZVBZJVDXRYHJEUPYFYISJYFJVHYISVHSJVIZYEYGXQYHXCXTEVJYJXSXCFU
      LZUBXPUOYHXQYJYKYAUBXPXCXTXSFVKVLXDYKKUBXPWTXSXCFVMVNVOVPVQXQSEVRYHJEVRVS
      YCVGXLUDUEZMUEZLUEZWTULZUMZMXRUOZLXRUPZUQZURTQUAUDQUDVIZTUAVIZWRYLXKYSYTV
      TUVAXJYRUVAWSXHUGZXIVBZRVDZYNXRUGZYQVBZLVDZXJYRUVAYNXHUGZWRYNWTULZUMZQXHU
      OZVBZLVDUVGUVDUVAUVLUVFLUVAUVHUVEUVKYQUVAXHXRYNUVAXGXQSEUVAXFXPURXGXQWAXE
      XOWBXDKXFXPWCWDWEZWFUVAXHXRURZUVKYQWAUVMUVNYPMXHUOYQUVKYPMXHXRWCUVJYPQMXH
      QMVIUVIYOWRYMYNWTVMWGVNVOWDVPWHUVCUVLRLUVCLVHUVLRVHRLVIZUVBUVHXIUVKWSYNXH
      VJUVOXBUVJQXHUVOXAUVIWSYNWRWTVKWGVLVPVQWIXIRXHVRYQLXRVRWJWKWLWMYDVGWN $.
      $([6-Apr-97]$)
  $}

$( End of set.mm $)

$( The work below may eventually be broken off as a separate
   module, dedekind.mm. $)

