$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u f g h $.  $( ` u g h ` are dummy. $)
    ac3a.1 $e |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $.
    $( Derivation of our AC from standard textbook AC.  Note that the
       Axiom of Regularity is required.  Equivalence is established
       using antepenultimate steps of proofs of ~ ac2 and ~ ac3 . $)
    ac3a $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu vg vh cv c0 wceq wn cfv wcel wi wral wex wel wa wrex wreu cpr cab
      wal hbra1 wmo ra4 fvex eleq1 biland birexdv bi2and cla4ev weq eqid cleq1
      binegd rcla4ev mpan22 fveq2 preq1 syl preq2 eqtrd imlan r19.22i zfpaircl
      cleq2 abcl sylibr visset pri2 pri1 pm3.2i jctir eleq2 sylan5 exp syl8
      imp3a pm2.43d eleq1d bitrd bi2imd rcla4v wb prel12 ancom syl6bb
      sylan9bbr eirrv mtbii con2 adantarr pm5.32d preleq syl6bir cleq2d
      biimparc syl6 exp4c com13 com4r imp imp4a com3l r19.23aiv sylbi exp3a
      imp4b 19.23adv df-rex syl5ib 19.21aiv mo2icl a1d jcad weu df-reu eu5
      bitr syl6ibr 19.21ai df-ral copab cvv fvopab cleq1i birex biab fvrexex
      eqeltrr pm3.27 ssab2i ssexi rexeq bireudv bilimd biraldv 19.23aiv ax-mp
      ) CKZLMZNZUVDFKZOZUVDPZQZCAKZRZFSUVFCETZDETZUAZEBKZUBZDUVDUCZQZCUVKRZBSZG
      UVLUWAFUVLUVFUVOEHKZLMZNZUWBUVGOZUWBUDZIKZMZUAZHUVKUBZIUEZUBZDUVDUCZQZCUV
      KRZUWAUVLCATZUWNQZCUFUWOUVLUWQCUVJCUVKUGUVLUWPUVFUWMUVLUWPUVFUAZDCTZUWLUA
      ZDSZUWTDUHZUAZUWMUVLUWRUXAUXBUVLUWRUXAUVLUWPUVFUWRUXAQZUVLUWPUVFUVIUXDUVJ
      CUVKUIUVIUWRUXAUVIUVMUVHEKZPZUAZEUWKUBZUXAUWRUWTUVIUXHUADUVHUVDUVGUJZDKZU
      VHMZUWSUVIUWLUXHUXJUVHUVDUKUXKUVOUXGEUWKUXKUVNUXFUVMUXJUVHUXEUKULUMUNUOUW
      RUVHUVDUDZUWKPZUVDUXLPZUVHUXLPZUAZUAUXHUWRUXMUXPUWRUWDUWFUXLMZUAZHUVKUBZU
      XMUWRUWDHCUPZUAZHUVKUBZUXSUWPUVFCCUPZUYBCUQUYAUVFUYCUAHUVDUVKUXTUWDUVFUXT
      UYCUXTUWCUVEUWBUVDLURUSUWBUVDUVDURUNUTVAUYAUXRHUVKUXTUXQUWDUXTUWFUVHUWBUD
      ZUXLUXTUWEUVHMUWFUYDMUWBUVDUVGVBUWEUVHUWBVCVDUWBUVDUVHVEVFVGVHVDUWJUXSIUX
      LUVHUVDVIUWGUXLMZUWIUXRHUVKUYEUWHUXQUWDUWGUXLUWFVJULUMVKVLUXNUXOUVHUVDCVM
      ZVNUVHUVDUXIVOVPVQUXGUXPEUXLUWKUXEUXLMUVMUXNUXFUXOUXEUXLUVDVRUXEUXLUVHVRU
      NUTVDVSVTWAWBWCUVLUXBUWRUVLUWTUXKQZDUFUXBUVLUYGDUVLUWSUWLUXKUVLUWSUWLUXKQ
      UVLUWSUAZUXEUWKPZUVOUAZESUXKUWLUYHUYJUXKEUVLUWSUYIUVOUXKUYIUWSUVLUVOUXKQZ
      UYIUWSUVLUYKUYIUWDUWFUXEMZUAZHUVKUBZUWSUVLUAZUYKQZUWJUYNIUXEEVMIEUPZUWIUY
      MHUVKUYQUWHUYLUWDUWGUXEUWFVJULUMVKUYMUYPHUVKUYOHATZUYMUYKUYOUYRUWDUYLUYKU
      WSUVLUYRUWDUYLUYKQZQQUVLUYRUWDUWSUYSUVLUYRUWDUWEUWBPZUWSUYSQUVJUWDUYTQCUW
      BUVKCHUPZUVFUWDUVIUYTVUAUVEUWCUVDUWBLURUSVUAUVIUWEUVDPUYTVUAUVHUWEUVDUVDU
      WBUVGVBZWDUVDUWBUWEVRWEWFWGUYLUWSUYTUYKUYLUWSUYTUVOUXKUYLUWSUYTUAZUVOUAZU
      XJUWEMZVUAUAZUXKUYLVUDVUCUXJUVDUDUWFMZUAVUFUYLVUCVUGUVOUYLVUCVUGUVOWHZUYL
      UWSVUHUYTUYLDCUPZNZVUHUWSVUJVUGUXJUWFPZUVDUWFPZUAZUYLUVOUXJUVDUWEUWBDVMZU
      YFUWBUVGUJZHVMZWIUYLVUMUVNUVMUAUVOUYLVUKUVNVULUVMUWFUXEUXJVRUWFUXEUVDVRUN
      UVNUVMWJWKWLVUIUWSVUIDDTUWSDWMUXJUVDUXJVRWNWOVSWPVTWQUXJUVDUWEUWBVUNUYFVU
      OVUPWRWSVUAUXKVUEVUAUVHUWEUXJVUBWTXAXBXCXDWAXEXFXGXHXIXJXKXDXLXMUVOEUWKXN
      XOVTWBXPUWTDUVHXQVDXRXSUWMUWTDXTUXCUWLDUVDYAUWTDYBYCYDXKYEUWNCUVKYFVLUVTU
      WOBUWKUWKUWHHUVKUBZIUEZUWBJKUXEUVGOZUXEUDZMEJYGZOZUWGMZHUVKUBZIUEVURYHVVD
      VUQIVVCUWHHUVKVVBUWFUWGEJUWBVUTUWFVUPUWEUWBVIEHUPZVUTUWEUXEUDZUWFVVEVUSUW
      EMVUTVVFMUXEUWBUVGVBVUSUWEUXEVCVDUXEUWBUWEVEVFYIYJYKYLHIUVKVVAAVMYMYNUWJV
      UQIUWIUWHHUVKUWDUWHYOVHYPYQUVPUWKMZUVSUWNCUVKVVGUVRUWMUVFVVGUVQUWLDUVDUVO
      EUVPUWKYRYSYTUVAUOVDUVBUVC $.
      $([17-Oct-96]$)
  $}

  ${
    $d x y z w v u $.
    $( Axiom of Choice using abbreviations.  Note:  The antepenultimate step
       of the proof shows the logical equivalence.  $)
    ac2 $p |- E. y A. z e. x A. w e. z E! v e. z E. u e. y
              ( z e. u /\ v e. u ) $=
      ( wel wa wex weq wb wal wi cv wrex wreu wral ax-ac 19.21v impexp bi2.04
      bitr bial weu df-eu df-reu 19.42v an41r3 anass bitr3 biex df-rex eleq1
      eleq2 bi2and cbvexv bilan 3bitr4 birbi bilim df-ral ax-17 hba1 hbex hbim
      birimd cbv4 19.5 mpbi ) ECGZCAGZHZVLEAGZABGZHHZAIZEAJZKZELZAIZMZCLELZBICF
      GZEFGZHZFBNZOZECNZPZDWHQZCANZQZBIBECARWBWLBWAELZCLVKWJMZCLWBWLWMWNCVKVJVT
      MZMZELVKWOELZMWMWNVKWOESWAWPEWAVJVKVTMMWPVJVKVTTVJVKVTUAUBUCWJWQVKDCGZWIM
      ZDLWRVTMZDLWJWQWSWTDWIVTWRVJWGHZEUDXAVQKZELZAIWIVTXAEAUEWGEWHUFVSXCAVRXBE
      VPXAVQVJVNVKVMHZHZHZAIVJXEAIZHVPXAVJXEAUGVOXFAVOVJVNHXDHXFVJVNVKVMUHVJVNX
      DUIUJUKWGXGVJWGFBGZWEHZFIXGWEFWFULXIXEFAFAJZXHVNWEXDFNZWKWFUMXJWCVKWDVMXK
      WKWHUNXKWKENZUNUOUOUPUBUQURUSUCUKURUTUCWIDWHVAWOWTEDWODVBWRVTEWREVBVSEAVR
      EVCVDVEEDJVJWRVTXLDNWHUMVFVGURUTURUCWAECVHWJCWKVAURUKVI $.
      $([18-Jul-96]$)
  $}

  ${
    $d x y z w v u $.  $( ` u ` is dummy. $)
    $( Axiom of Choice using abbreviations.  Note:  The antepenultimate step
       of the proof shows the logical equivalence. $)
    ac3 $p |- E. y A. z e. x ( -. z = (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu wel wa cv wrex wreu wral wex c0 wceq wn wi ac2 wal 19.23v df-ral
      nnull birim 3bitr4 biral biex mpbi ) CEGDEGHEBIJDCIZKZFUHLZCAIZLZBMUHNOPZ
      UIQZCUKLZBMABCFDERULUOBUJUNCUKFCGZUIQFSUPFMZUIQUJUNUPUIFTUIFUHUAUMUQUIFUH
      UBUCUDUEUFUG $.
      $([19-Jul-96]$)
  $}

  ${
    $d x z f y w v u g $.  $( ` y w v u g ` are dummy. $)
    $( Equivalent of Axiom of Choice.  Note that ` f ` is not necessarily a
       function.  Axiom of Choice, Takeuti and Zaring, p. 83. $)
    ac4 $p |- E. f A. z e. x ( -. z = (/) -> ( f ` z ) e. z ) $=
      ( vv vw vy vu vg cv c0 wceq wn wel wa wrex wreu wi wral wex cfv wcel ac3
      crab cuni copab wal weq cab eleq2 eleq1 birand birexdv bi2and biabdv
      df-rab 3eqtr4g unieqd cleqid visset zfausrab zfun2 opabval2 eleq1d reucl
      syl5bir syl3d a2i 19.20i df-ral 3imtr4 wmo moeq a1i funopabex fveq1
      bilimd biraldv cla4ev syl 19.23aiv ax-mp ) BIZJKLZBDMZEDMZNZDFIZOZEWBPZQZ
      BAIZRZFSWCWBCIZTZWBUAZQZBWKRZCSZAFBEDUBWLWRFWLWCWBGAMZHIGDMZWENZDWGOZEGIZ
      UCZUDZKZNGHUEZTZWBUAZQZBWKRZWRBAMZWJQZBUFXLXJQZBUFWLXKXMXNBXLWJXJXLWIXIWC
      XLXIWHEWBUCZUDZWBUAWIXLXHXPWBGHWBXEXPWKXGGBUGZXDXOXQEGMZXBNZEUHEBMZWHNZEU
      HXDXOXQXSYAEXQXRXTXBWHXCWBEIUIXQXAWFDWGXQWTWDWEXCWBDIUJUKULUMUNXBEXCUOWHE
      WBUOUPUQXGURXOWHEWBBUSUTVAVBVCWHEWBVDVEVFVGVHWJBWKVIXJBWKVIVJWQXKCXGXFGHW
      KAUSXFHVKWSHXEVLVMVNWMXGKZWPXJBWKYBWOXIWCYBWNXHWBWBWMXGVOVCVPVQVRVSVTWA
      $.
      $([21-Jul-96]$)
  $}

  ${
    $d x y f A $.  $( ` y ` is dummy. $)
    ac4c.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice (class version) $)
    ac4c $p |- E. f A. x e. A ( -. x = (/) -> ( f ` x ) e. x ) $=
      ( vy cv c0 wceq wn cfv wcel wi wral wex raleq biexdv ac4 vtoclbv ) AFZGHI
      SCFJSKLZAEFZMZCNTABMZCNEBDUABHUBUCCTAUABOPEACQR $.
      $([10-Feb-97]$)
  $}

  ${
    $d f x y w g A $.  $( ` y w g ` are dummy. $)
    ac5.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice. $)
    ac5 $p |- E. f ( f Fn A /\ A. x e. A ( -. x = (/) -> ( f ` x ) e. x ) ) $=
      ( vy vw vg cv c0 wceq wn cfv wcel wi wral wex wfn wa ac4c copab wal
      fveq2 cleqid fvex opabval2 eleq1d bilimd pm5.74i bial df-ral 3bitr4
      bilan weu eueq1 a1i fnopab mpbiran wmo moeq funopabex fneq1 fveq1
      biraldv bi2and cla4ev sylbir 19.23aiv ax-mp ) AHZIJKZVIEHZLZVIMZNZABOZEPC
      HZBQZVJVIVPLZVIMZNZABOZRZCPZABEDSVOWCEVOFHZBMZGHWDVKLZJZRFGTZBQZVJVIWHLZV
      IMZNZABOZRZWCWNWIVOWMVOWIVIBMZWLNZAUAWOVNNZAUAWMVOWPWQAWOWLVNWOWKVMVJWOWJ
      VLVIFGVIWFVLBWHWDVIVKUBWHUCZVIVKUDUEUFUGUHUIWLABUJVNABUJUKULWGFGBWHWGGUMW
      EGWFWDVKUDUNUOWRUPUQWBWNCWHWGFGBDWGGURWEGWFUSUOUTVPWHJZVQWIWAWMBVPWHVAWSV
      TWLABWSVSWKVJWSVRWJVIVIVPWHVBUFUGVCVDVEVFVGVH $.
      $([29-Aug-99]$)
  $}

  ${
    $d x f A $.
    ac5b.1 $e |- A e. V $.
    $( Equivalent of Axiom of Choice. $)
    ac5b $p |- ( A. x e. A -. x = (/) ->
               E. f ( f : A --> U. A /\ A. x e. A ( f ` x ) e. x ) ) $=
      ( cv c0 wceq wn wral wfn cfv wcel wi wa wex cuni wf ac5 19.42v an12 crn
      wss chfnrn exp anc2li df-f syl6ibr impac r19.26 abai pm3.27 sylbir
      r19.20i sylan5 sylbi 19.22i mpan2 ) AEZFGHZABIZCEZBJZUSURVAKURLZMZABIZNZC
      OZBBPZVAQZVCABIZNZCOZABCDRUTVGNUTVFNZCOVLUTVFCSVMVKCVMVBUTVENZNVKUTVBVETV
      BVJVKVNVBVJVIVBVJVBVAUAVHUBZNVIVBVJVOVBVJVOABVAUCUDUEBVHVAUFUGUHVNUSVDNZA
      BIVJUSVDABUIVPVCABVPUSVCNVCUSVCUJUSVCUKULUMULUNUOUPULUQ $.
      $([31-Aug-99]$)
  $}

  ${
    $d f g x y z w A $.  $d f g x y z w B $.  $d ph z w f $.  $d y ps $.
    ac6.1 $e |- A e. V $.
    ac6.2 $e |- B e. V $.
    ${
      $d z f g C $.  $d z f g H $.
      ac6lem.4 $e |- C = { y e. B | ph } $.
      ac6lem.5 $e |- H = { <. x , z >. | ( x e. A /\ z = C ) } $.
      $( Lemma for equivalent of Axiom of Choice. $)
      ac6lem $p |- ( A. x e. A E. y e. B ph ->
                   E. f ( f : A --> B /\ A. x e. A ( f ` x ) e. C ) ) $=
        ( vg wrex wral cv c0 wceq wn crn cuni wf cfv wcel wa wex wi crab
        cleq2i biimp cleq1d binegd rnoel3 syl6bb biimprcd r19.20i r19.23v
        sylib cab abid df-rex copab rneq ax-mp rnopab eqtr eleq2i 3bitr4r
        syl5ib r19.21aiv cvv wfn weu zfausrab eleq1i mpbir eueq1 a1i fnopab
        fnex mp2 rnexg ac5b ccom fnfrn mpbi fco mpan2 adantr wfun ax-17 wal
        hbopab1 bial 3imtr4 hbrn hbral hban fveq2 id eleq12d rcla4v isseti
        19.8a bitr4 sylibr exp comm eleq1 sylibd 19.23aiv syl5 imp adantall wb
        fnfun cdm fvco fndm sylan5br mpan12 fvopab2 fveq1 syl5eq fveq2d adantl
        eqtrd eleq1d adantalr mpbird r19.21ai ffun sylan jca wss ssuniss ssrab
        eqsstr sseq1 mpbiri r19.23aiv sylbi mpg fss visset coexg mp2an feq1
        wel hbco hbeq birald bi2and cla4ev syl 3syl ) ACFOZBEPZDQZRSZTZDIUAZPUW
        CUWCUBZNQZUCZUVTUWEUDZUVTUEZDUWCPZUFZNUGEFHQZUCZBQZUWKUDZGUEZBEPZUFZHUG
        ZUVSUWBDUWCUVSUVTGSZBEOZUWBUVTUWCUEZUVSUWSUWBUHZBEPUWTUWBUHUVRUXBBEUWSU
        WBUVRUWSUWBACFUIZRSZTUVRUWSUWAUXDUWSUVTUXCRUWSUVTUXCSGUXCUVTLUJUKULUMAC
        FUNUOUPUQUWSUWBBEURUSUWMEUEZUWSUFZBUGZUVTUXGDUTZUEZUWTUXAUXGDVAZUWSBEVB
        UWCUXHUVTUWCUXFBDVCZUAZUXHIUXKSZUWCUXLSMIUXKVDVEUXFBDVFVGVHZVIZVJVKDUWC
        NIVLUEZUWCVLUEEVLUEIEVMZUXPJUWSBDEIUWSDVNUXEDGGVLUEUXCVLUEACFKVOGUXCVLL
        VPVQZVRVSMVTZEVLIWAWBZIVLWCVEWDUWJUWRNUWJEFUWEIWEZUCZUWMUYAUDZGUEZBEPZU
        FZUWRUWCFUWEUCZUWIUYFUWFUYGUWIUFUYBUYEUYGUYBUWIUYGEUWCIUCZUYBUXQUYHUXSE
        IWFWGEUWCFUWEIWHWIWJUWEWKZUWIUYEUYGUYIUWIUFZUYDBEUYIUWIBUYIBWLUWHBDUWCB
        DIUVTUXKUEZUYKBWMUVTIUEZUYLBWMUXFBDDWNIUXKUVTMVHZUYLUYKBUYMWOWPZWQUWHBW
        LWRWSUYJUXEUYDUYJUXEUFUYDGUWEUDZGUEZUWIUXEUYPUYIUWIUXEUYPUWIGUWCUEZUYPU
        XEUWHUYPDGUWCUWSUWGUYOUVTGUVTGUWEWTUWSXAXBXCUWSDUGUXEUYQUHZDGUXRXDUWSUY
        RDUWSUXEUXAUYQUXEUWSUXAUXEUWSUXAUXFUXGUXAUXFBXEUXAUXIUXGUXNUXJXFXGXHXIU
        VTGUWCXJXKXLVEXMXNXOUYIUXEUYDUYPXPUWIUYIUXEUFZUYCUYOGUYSUYCUWMIUDZUWEUD
        ZUYOUYIIWKZUXEUYCVUASZUXQVUBUXSEIXQVEUYIVUBUFUWMIXRZUEVUCUXEUWMUWEIXSVU
        DEUWMUXQVUDESUXSEIXTVEVHYAYBUXEVUAUYOSUYIUXEUYTGUWEUXEUWMUXKUDZGUYTBDEG
        UXRYCUXMUYTVUESMUWMIUXKYDVEYEYFYGYHYIYJYKXHYLUWCFUWEYMYNYOUWFUWDFYPZUYG
        UXAUVTFYPZUHVUFDDUWCFYQUXAUWTVUGUXOUWSVUGBEUWSVUGUHUXEUWSVUGGFYPGUXCFLA
        CFYRYSUVTGFYTUVAVSUVBUVCUVDUWCUWDFUWEUVEWIYNUWQUYFHUYAUWEVLUEUXPUYAVLUE
        NUVFUXTUWEIVLVLUVGUVHUWKUYASZUWLUYBUWPUYEEFUWKUYAUVIVUHUWOUYDBEBDDUWKUY
        ADHUVJBWLBDUWEIDNUVJBWLUYNUVKUVLVUHUWNUYCGUWMUWKUYAYDYIUVMUVNUVOUVPXLUV
        Q $.
        $([18-Oct-99]$)
    $}

    ${
      ac6.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
      $( Equivalent of Axiom of Choice.  This is useful for proving that there
         exists, for example, a sequence mapping natural numbers to members of
         a large set ` B `, where ` ph ` depends on ` x ` (the
         natural number) and ` y ` (to specify a subset of ` B ` ).  Note: with
         some extra effort, we can also prove this when ` B ` is a proper class
         (should it ever become necessary).  To do this we observe that there
         exists a subset of ` B ` for which the theorem is true, implying it is
         also true for proper class ` B `. $)
      ac6 $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
        ( vz vw wrex wral cv wf cfv crab wcel wa wex wceq copab cleqid weq
        cleq1 biland cbvopab2v ac6lem rabcl pm3.27bd r19.20i imlan 19.22i syl
        ) ADFMCENEFGOZPZCOZUPQZADFRZSZCENZTZGUAUQBCENZTZGUAACDKEFUTGURESZLOZUTU
        BZTZCLUCHIUTUDVIVFKOZUTUBZTCLKLKUEVHVKVFVGVJUTUFUGUHUIVCVEGVBVDUQVABCEV
        AUSFSBABDUSFJUJUKULUMUNUO $.
        $([18-Oct-99]$)
    $}
  $}

  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x ( x e. On /\ ( f Fn x /\
                A. y ( y e. x -> ( f ` y ) = ( G ` ( f |` y ) ) ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` P~ A `. $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for Theorem 10.3 of Takeuti and Zaring, p. 84. $)
    numthlem   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 wceq wn cfv wcel wi cpw wral wex wf1o con wrex zfpowcl ac4c cima
      cdif cres wa tfr2 crn copab cvv wfun visset wfn tfr1 fnfun ax-mp
      resfunexg mp2 fvex rneq difeq2 fveq2 3syl fvopab fveq1 df-ima fveq2i
      3eqtr4 syl6eq eleq1d wss difss ssexi sselpw mpbi cleq1 binegd id eleq12d
      bi2imd rcla4v mpi imp syl5bir exp3a r19.21aivOBS tz7.49c f1oeq1 cla4ev
      r19.22i 19.23aiv ) BMZNOZPZWQFMZQZWQRZSZBCTZUAZFUBAMZCEMZUCZEUBZAUDUEZBXD
      FCIUFUGXEXJFXECGXFUHZUIZNOZPZXFGQZXLRZSZAUDUAXFCGXFUJZUCZAUDUEXJXEXQAUDXF
      UDRZXEXNXPXTXPXLWTQZXLRZXEXNUKXTXOYAXLXTXOXRHQZYAABADEGHJKULXRWQCXGUMZUIZ
      WTQZOEBUNZQZCXRUMZUIZWTQZYCYAEBXRYFYKXFUORGUPZXRUORAUQGUDURYLABDEGHJKUSZU
      DGUTVAGXFUOVBVCZYJWTVDXGXROYDYIOYEYJOYFYKOXGXRVEYDYICVFYEYJWTVGVHVIHYGOYC
      YHOLXRHYGVJVAXLYJWTXKYIOXLYJOGXFVKXKYICVFVAVLVMVNVOXEXNYBXEXLXDRZXNYBSZXL
      CVPYOCXKVQZXLCXLCIYQVRVSVTXCYPBXLXDWQXLOZWSXNXBYBYRWRXMWQXLNWAWBYRXAYAWQX
      LWQXLWTVGYRWCWDWEWFWGWHWIWJWKACGYMIWLXSXIAUDXHXSEXRYNXFCXGXRWMWNWOVHWPVA
      $.
      $([10-Feb-97]$)
  $}

  ${
    $d x y z w v u f g h A $.  $( ` y z w v u g h ` are dummy. $)
    numth.1 $e |- A e. V $.
    $( Numeration theorem:  any set can be put into 1-1 correspondence with some
       ordinal (using AC).  Theorem 10.3 of Takeuti and Zaring, p. 84. $)
    numth   $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vz vg vw vu vv vh cv con wcel wfn wel cfv cres crn cdif wceq copab
      wi wal wa wex cab cuni rfglem1 cleqid weq id rneq difeq2 fveq2 3syl
      cleqan12rd cbvopabv numthlem ) AEBFLZMNGLZUTOHFPHLZVAQVAVBRILZBJLZSZTZKLZ
      QZUAZJIUBZQUAUCHUDUEUEFUFGUGZCKVKUHZVJDFHAEGCVJUIVLUJVIELZBCLZSZTZVGQZUAJ
      ICEIEUKZJCUKZVCVMVHVQVRULVSVEVOUAVFVPUAVHVQUAVDVNUMVEVOBUNVFVPVGUOUPUQURU
      S $.
      $([10-Feb-97]$)

    $( Numeration theorem:  any set is equinumerous to some ordinal (using AC).
       Theorem 10.3  of Takeuti and Zaring, p. 84. $)
    numth2 $p |- E. x e. On x ~~ A $=
      ( vy cv cen wbr con wrex wf1o wex numth bren birex mpbir ) AEZBFGZAHIPBDE
      JDKZAHIABDCLQRAHPBDCMNO $.
      $([1-Apr-98]$)
  $}

  ${
    $d x y z w f A $.  $( ` y z w f ` are dummy. $)
    weth.1 $e |- A e. V $.
    $( Well-ordering theorem: any set can be well-ordered (using AC).
       Theorem 6 of Suppes, p. 242. $)
    weth   $p |- E. x x We A $=
      ( vy vf vz vw cv wf1o wex con wrex wwe numth wcel wi ccnv f1ocnv cep cfv
      wbr copab cleqid f1owe cxp cin weinxp xpex zfauscl2 weeq1 cla4ev sylbi
      syl6 word eloni ordwe syl syl5 19.23aiv comm r19.23aiv ax-mp ) DHZBEHZIZE
      JZDKLBAHZMZAJZDBECNVFVIDKVFVCKOZVIVEVJVIPZEVEBVCVDQZIZVKVCBVDRVMVCSMZVIVJ
      VMVNBFHVLTGHVLTSUAFGUBZMZVIFGBVCVOSVLVOUCUDVPBVOBBUEZUFZMZVIBVOUGVHVSAVRV
      QVOBBCCUHUIBVGVRUJUKULUMVJVCUNVNVCUOVCUPUQURUQUSUTVAVB $.
      $([9-Mar-97]$)
  $}


  ${
    $v t $.
    $v s $.
    $v r $.
    $v a $.
    $v b $.
    zornlem.vt $f set t $.
    zornlem.vs $f set s $.
    zornlem.vr $f set r $.
    zornlem.va $f set a $.
    zornlem.vb $f set b $.
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.
    $d h t f G $.
    $d t C $.  $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zornlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    zornlem.2 $e |- B = { f | E. h ( h e. On /\ ( f Fn h /\
                A. t ( t e. h -> ( f ` t ) = ( G ` ( f |` t ) ) ) ) ) } $.
    zornlem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zornlem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zornlem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A `. $)
    zornlem.6 $e |- G =
       { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.

    $( Lemma for Zorn's lemma. $)
    zornlem1 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con wcel wwe c0 wceq wn wa cfv wbr wral crab cuni cres tfr2 copab
      fveq1 ax-mp cvv wfun visset wfn tfr1 fnfun resfunexg mp2 wss cima ssrab
      sseq1i mpbir ssexi zfausrab zfun2 cab crn rneq df-ima syl6eqr eleq2d
      birimd biraldv2 rbiabdv 3eqtr4g bi2and biabdv df-rab unieqd fvopab eqtr
      syl6eq eleq1d wreu wereu mpan21 reucl syl syl5bir imp ) AUCZUDUEZFCUCZUFZ
      IUGUHUIZUJZXBNUKZIUEZXCXIEUCZDUCZXDULUIZEIUMZDIUNZUOZIUEZXGXCXHXOIXCXHNXB
      UPZOUKZXOMPAGKNORSUQXRXQPUCXLEHUMZDHUNZUOZUHKPURZUKZXOOYBUHXRYCUHUBXQOYBU
      SUTKPXQYAXOXBVAUENVBZXQVAUEAVCNUDVDYDMPGKNORSVEUDNVFUTNXBVAVGVHXNXMDIIFQI
      FVIZLUCZBUCJULZLNXBVJZUMZBFUNZFVIYIBFVKIYJFUAVLVMZVNZVOVPKUCZXQUHZXTXNYNX
      KHUEZXSUJZDVQXKIUEZXMUJZDVQXTXNYNYPYRDYNYOYQXSXMYNHIXKYNYGLYMVRZUMZBFUNYJ
      HIYNYTYIBFYNYGYGLYSYHYNYFYSUEYFYHUEYGYNYSYHYFYNYSXQVRYHYMXQVSNXBVTWAWBWCW
      DWETUAWFZWBYNXLXLEHIYNXJHUEXJIUEXLYNHIXJUVAWBWCWDWGWHXSDHWIXMDIWIWFWJWKWL
      WMWNXGXMDIWOZXPXEYEXFUVBYKDEFIXDYLWPWQXMDIWRWSWTXA $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem2 $p |- ( ( x e. On /\ ( w We A /\ -. D = (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con wcel wwe c0 wceq wn wa wel cfv cima wbr wi cdm wss onsst wfn
      tfr1 fndm ax-mp sseq2i sylibr wfun fnfun funfvima mpan syl adantr
      zornlem1 wral crab eleq2i breq2 biraldv rabcl bitr pm3.27bd breq1 rcla4v
      syld ) AUDZUEUFZGDUDUGJUHUIUJUKZUKZBAULZBUDZOUMZOWDUNZUFZWJWDOUMZKUOZWEWH
      WLUPZWFWEWDOUQZURZWOWEWDUEURWQWDUSWPUEWDOUEUTZWPUEUINQHLOPSTVAZUEOVBVCVDV
      EOVFZWQWOWRWTWSUEOVGVCWDWIOVHVIVJVKWGWMJUFZWLWNUPZACDEFGHIJKLMNOPQRSTUAUB
      UCVLXAMUDZWMKUOZMWKVMZXBXAWMGUFZXEXAWMXCCUDZKUOZMWKVMZCGVNZUFXFXEUKJXJWMU
      BVOXIXECWMGXGWMUIXHXDMWKXGWMXCKVPVQVRVSVTXDWNMWJWKXCWJWMKWAWBVJVJWC $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ -. D = (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con wcel wwe c0 wceq wn wa wel cfv wbr wi zornlem2 adantl breq1
      biimprcd poirr nsyli comm zornlem1 cima wral crab ssrab eqsstr ssi syl
      sylan5 syld ) GKUDZAUEZUFUGGDUEUHJUIUJUKULULZULBAUMZBUEOUNZVOOUNZKUOZVSVR
      UJZUKZVPVQVTUPVNABCDEFGHIJKLMNOPQRSTUAUBUCUQURVNVSGUGZVTWBUPVPVTVNWCULZWB
      VTWAVSVSKUOZWDWAWEVTVSVRVSKUSUTGVSKVAVBVCVPVSJUGWCACDEFGHIJKLMNOPQRSTUAUB
      UCVDJGVSJMUECUEKUOMOVOVEVFZCGVGGUBWFCGVHVIVJVKVLVM $.
      $([3-Apr-97]$)

    $( Lemma for Zorn's lemma. $)
    zornlem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa con wcel c0 wceq wex wrex crn cvv wn pm3.24 wi wal
      wss cfv ax-17 hba1 hban eleq1 zornlem1 wbr cima wral crab ssrab eqsstr
      ssi syl syl5bi comm exp32 a2d imp4a a4sd imp 19.23ad wfn wb tfr1 fvelrn
      ax-mp syl5ib ssrdv ssex exp adantl ccnv wfun wel zornlem3 exp45 com23
      19.21adv 19.20dv r2al syl6ibr cres ssid tz7.48lem mpan wrel cdm fnrel
      fndm sseq1i mpbir relssres mp2an cnveq funeq sylibr syl6 tz7.13 funrnex
      df-rn eleq1i dfdm4 eqtr3 3imtr4g mtoi jcad alnex syl5ibr mt3i df-rex ) FJ
      UDZFCUEUFZUGZAUEZUHUIZIUJUKZUGAULZYQAUHUMYNYRNUNZUOUIZYTUPZUGZYTUQYNYPYQU
      PZURZAUSZUVBYRUPYNUVEYTUVAYMUVEYTURYLYMUVEYTYMUVEUGZYSFUTYTUVFUCYSFUVFYPY
      ONVAZUCUEZUKZUGZAULZUVHFUIZUVHYSUIZUVFUVJUVLAYMUVEAYMAVBUVDAVCVDUVLAVBYMU
      VEUVJUVLURZYMUVDUVNAYMUVDYPUVIUVLYMYPUVCUVIUVLURZYPYMUVCUVOURYPYMUVCUVOUV
      IYPYMUVCUGUGZUVLUVIUVGFUIZUVLUVPUVGUVHFVEUVPUVGIUIUVQABCDEFGHIJKLMNOPQRST
      UAUBVFIFUVGILUEBUEJVGLNYOVHVIZBFVJFUAUVRBFVKVLVMVNVOVPVQVPVRVSVTWAWBNUHWC
      ZUVMUVKWDMPGKNORSWEZAUHUVHNWFWGWHWIYSFQWJVNWKWLYNUVENWMZWNZUVAYNUVEUVGUVH
      NVAUKUPZUCYOVIAUHVIZUWBYNUVEYPUCAWOZUGUWCURZUCUSZAUSUWDYNUVDUWGAYNUVDUWFU
      CYNUVDYPUWEUWCYNYPUVCUWEUWCURZYLYMYPUVCUWHURZURYLYPYMUWIYLYPYMUVCUWHAUCBC
      DEFGHIJKLMNOPQRSTUAUBWPWQWRWAVRVSWSWTUWCAUCUHYOXAXBUWDNUHXCZWMZWNZUWBUHUH
      UTZUWDUWLUHXDZAUCUHNUVTXEXFUWAUWKUKZUWBUWLWDNUWJUKZUWONXGZNXHZUHUTZUWPUVS
      UWQUVTUHNXIWGUWSUWMUWNUWRUHUHUVSUWRUHUKUVTUHNXJWGZXKXLNUHXMXNNUWJXOWGUWAU
      WKXPWGXQXRUWBYTUHUOUIZXSUWBUWAXHZUOUIZUWAUNZUOUIZYTUXAUXCUWBUXEUOUWAXTVPY
      SUXBUONYAYBUHUXDUOUWRUHUXDUWTNYCYDYBYEYFXRYGYPYQAYHYIYJYQAUHYKXQ $.
      $([3-Apr-97]$)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y `. $)
      zornlem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for Zorn's lemma. $)
      zornlem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) ->
            ( F " x ) (_ A ) $=
        ( zornlem.vs cv wwe con wcel wa c0 wceq wn wral cima wel cfv wex ax-17
        hbra1 hban wi wal eleq1 zornlem1 wbr crab ssrab eqsstr ssi syl syl5bi
        ontrt syland comm exp43 com13 imp a2d a4sd df-ral syl5ib imp4b 19.23ad
        wfun wfn tfr1 fnfun ax-mp fvelima mpan syl5 ssrdv ) GDUGUHZAUGZUIUJZUKZ
        QULUMUNZBWPUOZUKZUFOWPUPZGXABAUQZBUGZOURZUFUGZUMZUKZBUSZXFGUJZXFXBUJZXA
        XHXJBWRWTBWRBUTWSBWPVAVBXJBUTWRWTXCXGXJWRXCWSVCZBVDXCXGXJVCZVCZWTWRXLXN
        BWRXCWSXMWOWQXCWSXMVCZVCXCWQWOXOXCWQWOWSXMXGXCWQUKZWOWSUKZUKXJXGXDUIUJZ
        XQXJXPXGXEGUJZXJXRXQUKZXEXFGVEXTXEQUJXSBCDEFGHIQKLMNOPRSTUAUBUEUDVFQGXE
        QMUGCUGKVGMOXDUPUOZCGVHGUEYACGVIVJVKVLVMXDWPVNVOVPVQVRVSVTWAWSBWPWBWCWD
        WEOWFZXKXIOUIWGYBNRHLOPTUAWHUIOWIWJBXFWPOWKWLWMWN $.
        $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x -. H = (/) ) -> R Or ( F " x ) ) ) $=
        ( zornlem.vr zornlem.vs zornlem.va zornlem.vb wpo cv wwe con wcel wa
        c0 wceq wn wral cima wbr weq w3o wor wss wi zornlem5 poss syl comm wal
        wel cfv wex crab anandir ontrt im2an sylbi ancoms exp cleqid zornlem2
        adantall breq12 biimpcd syl6 com23 pm3.27 imlan sylan5 imp cleq12
        fveq2 syl5bi adantl adantalr wb pm3.26 im3ord word tz7.10 eloni syl2an
        syl5 exp31 com4r pm2.43i exp4a com3r a2d cleq1i bineg biral imaeq2
        raleq rbiabdv cleq1d binegd rcla4v im2and imp4a 19.23advv wfun wfn
        tfr1 fnfun ax-mp fvelima an4 bi2ex eeanv bitr sylibr 19.21aivv r2al
        a1i jcad df-so syl6ibr ) GKUJZGDUKULZAUKZUMUNZUOZQUPUQZURZBUVCUSZUOZOUV
        CUTZKUJZUFUKZUGUKZKVAZUFUGVBZUVMUVLKVAZVCZUGUVJUSUFUVJUSZUOUVJKVDUVAUVI
        UVKUVRUVIUVAUVKUVIUVJGVEUVAUVKVFABCDEFGHIJKLMNOPQRSTUAUBUCUDUEVGUVJGKVH
        VIVJUVIUVRVFUVAUVIUVLUVJUNZUVMUVJUNZUOZUVQVFZUGVKUFVKUVRUVIUWBUFUGUVIUH
        AVLZUIAVLZUOZUHUKZOVMZUVLUQZUIUKZOVMZUVMUQZUOZUOZUIVNUHVNZUVQUWAUVIUWMU
        VQUHUIUVEUVHUWMUVQVFUVEUVHUWEUWLUVQUVEUWEMUKCUKKVAZMOUWFUTZUSZCGVOZUPUQ
        ZURZUWOMOUWIUTZUSZCGVOZUPUQZURZUOZVFZUWEUWLUVQVFZVFUVHUVEUWEUXFUXHUVBUV
        DUWEUXFUXHVFZVFUVDUWEUVBUXIUVDUWEUVBUXFUXHUVDUWEUWFUMUNZUWIUMUNZUOZUVBU
        XFUOZUXHVFZUVDUWEUXLUWEUVDUXLUWEUVDUOUWCUVDUOZUWDUVDUOZUOUXLUWCUWDUVDVP
        UXOUXJUXPUXKUWFUVCVQUWIUVCVQVRVSVTWAUXLUXNUXLUXMUWLUXLUVQUXLUXMUWLUXLUV
        QVFUXLUXMUOZUWLUOZUHUIVLZUHUIVBZUIUHVLZVCZUVQUXLUXRUXSUVNUXTUVOUYAUVPUX
        QUWLUXSUVNVFZUXLUVBUXEUOZUWLUYCVFUXMUXLUYDUOZUXSUWLUVNUYEUXSUWGUWJKVAZU
        WLUVNVFUXKUYDUXSUYFVFUXJUIUHCDEFGHIUXCKLMNOPRSTUAUBUXCWBUDWCWDUWLUYFUVN
        UWGUVLUWJUVMKWEWFWGWHUXFUXEUVBUWTUXEWIWJWKWLUWLUXTUVOVFUXQUWLUWGUWJUQUV
        OUXTUWGUVLUWJUVMWMUWFUWIOWNWOWPUXQUWLUYAUVPVFZUXLUVBUWTUOZUWLUYGVFUXMUX
        LUYHUOZUYAUWLUVPUYIUYAUWJUWGKVAZUWLUVPVFUXJUYHUYAUYJVFUXKUHUICDEFGHIUWR
        KLMNOPRSTUAUBUWRWBUDWCWQUWLUYJUVPUWKUWHUYJUVPWRUWJUVMUWGUVLKWEVTWFWGWHU
        XFUWTUVBUWTUXEWSWJWKWLWTUWFXAUWIXAUYBUXJUXKUWFUWIXBUWFXCUWIXCXDXEXFXGXH
        WGXIXJWLXKUVHUWOMOBUKZUTZUSZCGVOZUPUQZURZBUVCUSZUXGUVGUYPBUVCUVFUYOQUYN
        UPUEXLXMXNUYQUWCUWTUWDUXEUYPUWTBUWFUVCBUHVBZUYOUWSUYRUYNUWRUPUYRUYMUWQC
        GUYRUYLUWPUQUYMUWQWRUYKUWFOXOUWOMUYLUWPXPVIXQXRXSXTUYPUXEBUWIUVCBUIVBZU
        YOUXDUYSUYNUXCUPUYSUYMUXBCGUYSUYLUXAUQUYMUXBWRUYKUWIOXOUWOMUYLUXAXPVIXQ
        XRXSXTYAVSXEYBWLYCUWAUWCUWHUOZUHVNZUWDUWKUOZUIVNZUOZUWNOYDZUWAVUDVFOUMY
        EVUENRHLOPTUAYFUMOYGYHVUEUVSVUAUVTVUCVUEUVSVUAUHUVLUVCOYIWAVUEUVTVUCUIU
        VMUVCOYIWAYAYHUWNUYTVUBUOZUIVNUHVNVUDUWMVUFUHUIUWCUWDUWHUWKYJYKUYTVUBUH
        UIYLYMYNXEYOUVQUFUGUVJUVJYPYNYQYRUFUGUVJKYSYT $.
        $([4-Apr-97]$)

      $( Lemma for Zorn's lemma. $)
      zornlem7 $p |- ( ( R Po A /\
      A. s ( ( s (_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
        ( wpo cv wss wor wa wbr weq wo wral wrex wi wal wwe wex wn weth c0
        wceq con zornlem4 cima crab wb imaeq2 raleq syl rbiabdv 3eqtr4g cleq1d
        onminex wcel zornlem5 a1i zornlem6 jcad cvv wfun visset wfn tfr1 fnfun
        ax-mp zfrep5 mp2 sseq1 soeq2 bi2and birexdv bi2imd cla4v sylan9
        adantld imp noel ssd w3a potr 3anass sylan5br exp3a com23 breq1
        biimprcd adantl jaod exp42 sylan9r com24 imp31 a2d 19.20dv df-ral
        3imtr4g cleq1i eleq2 sylbi breq2 biraldv rabcl syl5bbr biimpd rcbvalv
        syl5ib exp32 com34 mtoi exp4a exp com4r pm2.43a imp3a com4l 19.21adv
        syl6ibr imdistan 19.22dv df-rex comm adantr imp32 mpd exp45 imp4a
        com3l r19.23aiv 3syl adantalr pm2.43i 19.23adv mpi ) GKUJZSUKZGULZUWAKU
        MZUNZTUKZUAUKZKUOZTUAUPZUQZTUWAURZUAGUSZUTZSVAZUNZGDUKVBZDVCUWFUBUKZKUO
        ZVDZUBGURZUAGUSZDGUCVEUWNUWOUWTDUWNUWOUWTUWNUWOUNZUWTUVTUWOUXAUWTUTZUWM
        UVTUWOUNJVFVGZAVHUSUXCQVFVGZVDBAUKZURZUNZAVHUSUXBACDEFGHIJKLMNOPRUCUDUE
        UFUGUHVIUXCUXDABABUPZJQVFUXHMUKZCUKZKUOZMOUXEVJZURZCGVKZUXKMOBUKZVJZURZ
        CGVKJQUXHUXMUXQCGUXHUXLUXPVGUXMUXQVLUXEUXOOVMUXKMUXLUXPVNVOVPUGUIVQVRVS
        UXGUXBAVHUXAUXEVHVTZUXGUWTUXAUXRUXCUXFUWTUWNUWOUXRUXCUXFUWTUTZUTZUTUWNU
        WOUXRUXTUWNUXCUWOUXRUNZUXSUWNUXCUYAUXFUWTUWNUXCUYAUXFUNZUNZUNUWITUXLURZ
        UAGUSZUWTUWNUYCUYEUWNUYBUYEUXCUVTUYBUXLGULZUXLKUMZUNZUWMUYEUVTUYBUYFUYG
        UYBUYFUTUVTABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWAZWBABCDEFGHIJKLMNOPQRUCUDU
        EUFUGUHUIWCWDUWLUYHUYEUTSUXLUXEWEVTOWFZUXLWEVTAWGOVHWHUYJNRHLOPUDUEWIVH
        OWJWKOUXEWEWLWMUWAUXLVGZUWDUYHUWKUYEUYKUWBUYFUWCUYGUWAUXLGWNUWAUXLKWOWP
        UYKUWJUYDUAGUWITUWAUXLVNWQWRWSWTXAXBUWNUXCUYBUYEUWTUTZUVTUXCUYBUYLUTZUT
        UWMUXCUVTUYMUXCUVTUYBUYLUXCUVTUYBUNZUNZUWFGVTZUYDUNZUAVCUYPUWSUNZUAVCUY
        EUWTUYOUYQUYRUAUYOUYPUYDUWSUTUTUYQUYRUTUYOUYPUYDUWSUYOUYQUWPGVTZUWRUTZU
        BVAUWSUYOUYQUYTUBUYOUYPUYDUYTUYSUYOUYPUYDUWRUYSUXCUYNUYPUYDUWRUTZUTZUXC
        UYSUYNVUBUTUXCUYSUYNUYSVUBUXCUYSUYNUYSVUBUTUTUXCUYSUNZUYNUYPUYSVUAVUCUY
        NUYPUYSVUAVUCUYNUYPUYSUNZUYDUWRVUCUYNVUDUNZUNUYDUNUWQUWPVFVTZUWPXCVUCVU
        EUYDUWQVUFUTVUCVUEUWQUYDVUFVUCVUEUWQUYDVUFUTVUEUWQUNZUYDUWEUWPKUOZTUXLU
        RZVUCVUFVUGUWEUXLVTZUWIUTZTVAVUJVUHUTZTVAUYDVUIVUGVUKVULTVUGVUJUWIVUHUY
        NVUDUWQVUJUWIVUHUTZUTZUYNUWQVUDVUNUYNVUJVUDUWQVUMUYBVUJUWEGVTZUVTVUDUWQ
        VUMUTUTUYBUXLGUWEUYIXDUVTVUOVUDUWQVUMUVTVUOVUDUNZUNZUWQUNUWGVUHUWHVUQUW
        QUWGVUHUTVUQUWGUWQVUHVUQUWGUWQVUHUVTVUOUYPUYSXEUWGUWQUNVUHUTVUPGUWEUWFU
        WPKXFVUOUYPUYSXGXHXIXJXBUWQUWHVUHUTVUQUWHVUHUWQUWEUWFUWPKXKXLXMXNXOXPXQ
        XJXRXSXTUWITUXLYAVUHTUXLYAYBVUCUXIUWPKUOZMUXLURZVUFVUIUXCUYSVUSVUFUTUXC
        UYSVUSVUFUXCUYSVUSUNZVUFUXCUWPUXNVTZVUFVUTUXCUXNVFVGVVAVUFVLJUXNVFUGYCU
        XNVFUWPYDYEUXMVUSCUWPGCUBUPUXKVURMUXLUXJUWPUXIKYFYGYHYIYJXIXBVUHVURTMUX
        LUWEUXIUWPKXKYKYLXPYMYNXRYOXOYPYNYQYRYSYTUVAYTUVBUWRUBGYAUVCXIUYPUYDUWS
        UVDVOUVEUYDUAGUVFUWSUAGUVFYBYMUVGUVHUVIUVJUVKXJXIXBUVLUVMUVNUVOUVPUVQYQ
        UVRUVS $.
        $([6-Apr-97]$)
    $}

  $}

  ${
    $v t $.
    $v s $.
    $v r $.
    $v q $.
    $v a $.
    $v b $.
    $v c $.
    $v d $.
    $v j $.
    $v k $.
    $v m $.
    $v n $.
    zorn.vt $f set t $.
    zorn.vs $f set s $.
    zorn.vr $f set r $.
    zorn.vq $f set q $.
    zorn.va $f set a $.
    zorn.vb $f set b $.
    zorn.vc $f set c $.
    zorn.vd $f set d $.
    zorn.vj $f set j $.
    zorn.vk $f set k $.
    zorn.vm $f set m $.
    zorn.vn $f set n $.
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
      $( ` v u f g h t s r q a b c d j k m n ` are dummy. $)
    zorn.1 $e |- A e. V $.
    $( Zorn's Lemma, Monk1, p. 117.  Requires Axiom of Choice.  The main
       proof is in ~ zornlem7 ; this proof mainly changes bound variables
       to eliminate the hypotheses of ~ zornlem7 . $)
    zorn   $p |- ( ( R Po A /\
      A. w ( ( w (_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( zorn.vt vu zorn.vr zorn.vq zorn.vn zorn.vj zorn.vb zorn.va zorn.vc
      zorn.vk zorn.vm vv vh zorn.vd zorn.vs vf vg cv con wcel wfn wel cfv cres
      wbr wn crn wral crab cuni wceq copab wi wal wa wex cab cima rfglem1
      cleqid weq breq2 biraldv breq1 rcbvalv syl5bb cbvrabv id wb rneq raleq
      syl rbiabdv eleq2d binegd bi2and biabdv eleq1 cbvabv syl5eq df-rab
      3eqtr4g unieqd cleqan12rd cbvopabv zornlem7 ) HIJKLMENUEZUFUGOUEZWNUHPNUI
      PUEZWOUJWOWPUKQUEZWQRUEZKUEZULZUMZQWSSUEZFULZKTUEZUNZUOZSEUPZUOZRXGUPZUQZ
      URZTQUSZUJURUTPVAVBVBNVCOVDZXCKUAUEZUNZUOZSEUPZUBUEZJUEZFULZUBXMUQZHUEVEU
      OJEUPZFUAUBUCYAXLXTUBYAIUEVEUOJEUPZUDDCABGNPUCUDOUAXLVFYAVGXPXTUBXOUOZSJE
      SJVHZXRXBFULZUBXOUOYDXPYEYFXTUBXOXBXSXRFVIVJXCYFKUBXOWSXRXBFVKVLVMVNYBVGX
      KUDUEZMUEZLUEZWSULZUMZMXQUOZLXQUPZUQZURTQUAUDQUDVHZTUAVHZWQYGXJYNYOVOYPXI
      YMYPWRXGUGZXHVBZRVDZYIXQUGZYLVBZLVDZXIYMYPYIXGUGZWQYIWSULZUMZQXGUOZVBZLVD
      UVBYSYPUVGUVALYPUVCYTUVFYLYPXGXQYIYPXFXPSEYPXEXOURXFXPVPXDXNVQXCKXEXOVRVS
      VTZWAYPXGXQURZUVFYLVPUVHUVIYKMXGUOYLUVFYKMXGXQVRUVEYKQMXGQMVHUVDYJWQYHYIW
      SVKWBVLVMVSWCWDYRUVGRLRLVHZYQUVCXHUVFWRYIXGWEUVJXAUVEQXGUVJWTUVDWRYIWQWSV
      IWBVJWCWFWGXHRXGWHYLLXQWHWIWJWKWLYCVGWM $.
      $([6-Apr-97]$)
  $}

$( End of set.mm $)

$( The work below may eventually be broken off as a separate
   module, dedekind.mm. $)

