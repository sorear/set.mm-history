$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The intersection of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class intersection symbol. $)
  $c |^| $. $( Big cap $)

  $( Extend class notation to include the intersection of a class (read:
     'intersect ` A ` '). $)
  cint $a class |^| A $.

  ${
    $d x y A $.
    $( Define the intersection of a class.  Definition 7.35, p. 44, of Takeuti
       and Zaring. $)
    df-int $a |- |^| A = { x | A. y ( y e. A -> x e. y ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class intersection. $)
    dfint2 $p |- |^| A = { x | A. y e. A x e. y } $=
      ( cint cv wcel wel wi wal cab wral df-int df-ral biab eqtr4 ) CDBECFABGZH
      BIZAJPBCKZAJABCLRQAPBCMNO $.
      $([28-Jun-98]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Equality law for intersection. $)
    inteq $p |- ( A = B -> |^| A = |^| B ) $=
      ( vx vy wceq wel wral cab cint raleq biabdv dfint2 3eqtr4g ) ABEZCDFZDAGZ
      CHODBGZCHAIBINPQCODABJKCDALCDBLM $.
      $([13-Sep-99]$)
  $}

  ${
    $d x A y $.  $d x B y $.  $( ` y ` is dummy. $)
    elint.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint   $p |- ( A e. |^| B <-> A. x ( x e. B -> A e. x ) ) $=
      ( vy cv wcel wel wi wal cint wceq eleq1 bilimd bialdv df-int vtoclab ) AF
      ZCGZEAHZIZAJSBRGZIZAJEBCKDEFZBLZUAUCAUETUBSUDBRMNOEACPQ $.
      $([21-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.
    elint2.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint2   $p |- ( A e. |^| B <-> A. x e. B A e. x ) $=
      ( cint wcel cv wi wal wral elint df-ral bitr4 ) BCEFAGZCFBNFZHAIOACJABCDK
      OACLM $.
      $([14-Oct-99]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Membership in class intersection. $)
    elinti   $p |- ( A e. |^| B -> ( C e. B -> A e. C ) ) $=
      ( vy vx cint wcel cv wi wal wel wceq eleq1 bilimd bialdv visset elint
      biimp vtoclga eleq2 bi2imd cla4gv pm2.43b syl ) ABFZGDHZBGZAUFGZIZDJZCBGZ
      ACGZIZUGEDKZIZDJZUJEAUEEHZALZUOUIDURUNUHUGUQAUFMNOUQUEGUPDUQBEPQRSUJUKULU
      IUMDCBUFCLUGUKUHULUFCBMUFCATUAUBUCUD $.
      $([14-Oct-99]$)
  $}

  ${
    $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbint.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound variable hypothesis builder for intersection. $)
    hbint   $p |- ( y e. |^| A -> A. x y e. |^| A ) $=
      ( vz cv wcel wel wi wal cint ax-17 hbel hbim hbal visset elint bial
      3imtr4 ) EFZCGZBEHZIZEJZUDAJBFZCKGZUFAJUCAEUAUBAABBTCUBALZDMUGNOEUECBPQZU
      FUDAUHRS $.
      $([2-Feb-97]$)
  $}

  ${
    $d A x y $.  $d ph y $.  $( ` y ` is dummy. $)
    inteqab.1 $e |- A e. V $.
    $( Membership in the intersection of an abstraction class. $)
    inteqab   $p |- ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) $=
      ( vy cab cint wcel cv wi wal elint hbab1 ax-17 hbim weq eleq1 abid
      syl6bbr eleq2 bi2imd cbv4 bitr ) CABFZGHEIZUDHZCUEHZJZEKACBIZHZJZBKECUDDL
      UHUKEBUFUGBABEMUGBNOUKENEBPZUFAUGUJULUFUIUDHAUEUIUDQABRSUEUICTUAUBUC $.
      $([30-Aug-93]$)

    $( Membership in the intersection of an abstraction class. $)
    inteqrab   $p |- ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) $=
      ( cv wcel wa cab cint wi wal crab wral inteqab impexp bial bitr wceq
      df-rab inteq ax-mp eleq2i df-ral 3bitr4 ) CBFZDGZAHZBIZJZGZUGACUFGZKZKZBL
      ZCABDMZJZGUMBDNUKUHULKZBLUOUHBCEOURUNBUGAULPQRUQUJCUPUISUQUJSABDTUPUIUAUB
      UCUMBDUDUE $.
      $([17-Oct-99]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The intersection of the empty set is the universal class.  Exercise 2,
       p. 44 of Takeuti and Zaring.  $)
    int0 $p |- |^| (/) = V $=
      ( vy vx cv c0 wcel wel wi wal cab weq cint cvv noel pm2.21i ax-gen eqid
      2th biab df-int df-v 3eqtr4 ) ACZDEZBAFZGZAHZBIBBJZBIDKLUFUGBUFUGUEAUCUDU
      BMNOBPQRBADSBTUA $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( An element of a class includes the intersection of the class.  Exercise
       4, p. 44 of Takeuti and Zaring (with correction), generalized to
       classes. $)
    intss $p |- ( A e. B -> |^| B (_ A ) $=
      ( vx vy wcel cint cv wel wi wal wceq eleq1 eleq2 bi2imd cla4gv pm2.43a
      visset elint syl5ib ssrdv ) ABEZCBFZAUADGZBEZCDHZIZDJZCGZAEZUHUBEUGUAUIUF
      UAUIIDABUCAKUDUAUEUIUCABLUCAUHMNOPDUHBCQRST $.
      $([18-Nov-95]$)

    $( Subclass of a class intersection. $)
    ssint $p |- ( A (_ |^| B <-> A. x e. B A (_ x ) $=
      ( vy cint wss cv wcel wral wel dfss2 visset elint2 biral ralcom2 bitr4
      3bitr ) BCEZFDGZRHZDBIDAJZACIZDBIZBAGZFZACIZDBRKTUBDBASCDLMNUCUADBIZACIUF
      UADABCOUEUGACDBUDKNPQ $.
      $([14-Oct-99]$)

    $( Subclass of an upper bound. $)
    ssintub $p |- A (_ |^| { x e. B | A (_ x } $=
      ( vy cv wss crab cint wral wcel wa sseq2 rabcl pm3.27 sylbi rgen ssint
      mpbir ) BBAEZFZACGZHFBDEZFZDUAIUCDUAUBUAJUBCJZUCKUCTUCAUBCSUBBLMUDUCNOPDB
      UAQR $.
      $([8-Aug-00]$)

    $( Intersection of subclasses. $)
    ssintss $p |- ( A (_ B -> |^| B (_ |^| A ) $=
      ( vy vx cv wcel wi wal cint wss wel syl2 19.20ii visset elint 3imtr4g
      19.21aiv df-ss 3imtr4 ) CEZAFZTBFZGZCHZDEZBIZFZUEAIZFZGZDHABJUFUHJUDUJDUD
      UBDCKZGZCHUAUKGZCHUGUIUCULUMCUAUBUKLMCUEBDNZOCUEAUNOPQCABRDUFUHRS $.
      $([14-Oct-99]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The intersection of a non-empty class exists.  Exercise 5, p. 44 of
       Takeuti and Zaring. $)
    intex $p |- ( -. A = (/) -> |^| A e. V ) $=
      ( vx c0 wceq wn cv wcel wex cint cvv nnull wss intss visset ssex syl
      19.23aiv sylbi ) ACDEBFZAGZBHAIZJGZBAKTUBBTUASLUBSAMUASBNOPQR $.
      $([23-Aug-93]$)
  $}

  ${
    $d x y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( The intersection of a non-empty abstraction class exists. $)
    intexab $p |- ( E. x ph -> |^| { x | ph } e. V ) $=
      ( wex cab c0 wceq wn cint cvv wcel noel3 intex sylbir ) ABCABDZEFGNHIJABK
      NLM $.
      $([30-Aug-93]$)
  $}

  ${
    $( The intersection of a non-empty restricted abstraction class exists. $)
    intexrab $p |- ( E. x e. A ph -> |^| { x e. A | ph } e. V ) $=
      ( cv wcel wa wex cab cint cvv wrex crab intexab df-rex wceq df-rab inteq
      ax-mp eleq1i 3imtr4 ) BDCEAFZBGUABHZIZJEABCKABCLZIZJEUABMABCNUEUCJUDUBOUE
      UCOABCPUDUBQRST $.
      $([13-Sep-99]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    intpr.1 $e |- A e. V $.
    intpr.2 $e |- B e. V $.
    $( The intersection of a pair is the intersection of its members.  Theorem
       71 of Suppes, p. 42. $)
    intpr   $p |- |^| { A , B } = ( A i^i B ) $=
      ( vx vy cpr cint cin cv wcel wel wi wal wa wceq 19.26 wo visset elpr
      birim jaob bitr bial clel4 bi2an 3bitr4 elint elin cleqri ) EABGZHZABIZFJ
      ZUKKZEFLZMZFNZEJZAKZUSBKZOZUSULKUSUMKUNAPZUPMZUNBPZUPMZOZFNVDFNZVFFNZOURV
      BVDVFFQUQVGFUQVCVERZUPMVGUOVJUPUNABFSTUAVCUPVEUBUCUDUTVHVAVIFUSACUEFUSBDU
      EUFUGFUSUKESUHUSABUIUGUJ $.
      $([14-Oct-99]$)
  $}


