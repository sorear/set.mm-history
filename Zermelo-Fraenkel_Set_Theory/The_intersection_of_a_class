$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The intersection of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class intersection symbol. $)
  $c |^| $. $( Big cap $)

  $( Extend class notation to include the intersection of a class (read:
     'intersect A'). $)
  cint $a class |^| A $.

  ${
    $d x y A $.
    $( Define the intersection of a class.  Definition 7.35, p. 44, of Takeuti
       and Zaring. $)
    df-int $a |- |^| A = { x | A. y ( y e. A -> x e. y ) } $.
  $}

  ${
    $d x A y $.  $d x B y $.  $( ` y ` is dummy. $)
    elint.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint   $p |- ( A e. |^| B <-> A. x ( x e. B -> A e. x ) ) $=
      ( vy cv wcel wel wi wal cint wceq cleq3 bilimd bialdv df-int vtoclab )
      AFZCGZEAHZIZAJSBRGZIZAJEBCKDEFZBLZUAUCAUETUBSUDBRMNOEACPQ $.
      $([21-May-94]$)
  $}

  ${
    $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbint.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound variable hypothesis builder for intersection. $)
    hbint   $p |- ( y e. |^| A -> A. x y e. |^| A ) $=
      ( vz cv wcel wel wi wal cint ax-17 hbel hbim hbal visset elint bial
      3imtr4 ) EFZCGZBEHZIZEJZUDAJBFZCKGZUFAJUCAEUAUBAABBTCUBALZDMUGNOEUECBPQZU
      FUDAUHRS $.
      $([2-Feb-97]$)
  $}

  ${
    $d A x y $.  $d ph y $.  $( ` y ` is dummy. $)
    inteqab.1 $e |- A e. V $.
    $( Membership in the intersection of an abstraction class. $)
    inteqab   $p |- ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) $=
      ( vy cab cint wcel cv wi wal elint hbab1 ax-17 hbim weq cleq3 abid
      syl6bbr cleq4 bi2imd cbv4 bitr ) CABFZGHEIZUDHZCUEHZJZEKACBIZHZJZBKECUDD
      LUHUKEBUFUGBABEMUGBNOUKENEBPZUFAUGUJULUFUIUDHAUEUIUDQABRSUEUICTUAUBUC $.
      $([30-Aug-93]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The intersection of the empty set is the universal class.  Exercise 2,
       p. 44 of Takeuti and Zaring.  $)
    int0 $p |- |^| (/) = V $=
      ( vy vx cv c0 wcel wel wi wal cab weq cint cvv noel pm2.21i ax-gen eqid
      2th biab df-int df-v 3eqtr4 ) ACZDEZBAFZGZAHZBIBBJZBIDKLUFUGBUFUGUEAUCUDU
      BMNOBPQRBADSBTUA $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( An element of a class includes the intersection of the class.  Exercise
       4, p. 44 of Takeuti and Zaring (with correction), generalized to
       classes. $)
    intss $p |- ( A e. B -> |^| B (_ A ) $=
      ( vx vy wcel cint cv wel wi wal wceq cleq3 cleq4 bi2imd cla4gv pm2.43a
      visset elint syl5ib ssrdv ) ABEZCBFZAUADGZBEZCDHZIZDJZCGZAEZUHUBEUGUAUIU
      FUAUIIDABUCAKUDUAUEUIUCABLUCAUHMNOPDUHBCQRST $.
      $([18-Nov-95]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The intersection of a non-empty class exists.  Exercise 5, p. 44 of
       Takeuti and Zaring. $)
    intex $p |- ( -. A = (/) -> |^| A e. V ) $=
      ( vx c0 wceq wn cv wcel wex cint cvv nnull wss intss visset ssex syl
      19.23aiv sylbi ) ACDEBFZAGZBHAIZJGZBAKTUBBTUASLUBSAMUASBNOPQR $.
      $([23-Aug-93]$)
  $}

  ${
    $d x y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( The intersection of a non-empty abstraction class exists. $)
    intexab $p |- ( E. x ph -> |^| { x | ph } e. V ) $=
      ( vy wex cab c0 wceq wn cint cvv wcel cv hbab1 ax-17 cleq3 cbvex nnull
      abid biex 3bitr4 intex sylbir ) ABDZABEZFGHZUDIJKCLZUDKZCDBLZUDKZBDUEUCUG
      UICBABCMUICNUFUHUDOPCUDQAUIBABRSTUDUAUB $.
      $([30-Aug-93]$)
  $}



