$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The intersection of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class intersection symbol. $)
  $c |^| $. $( Big cap $)

  $( Extend class notation to include the intersection of a class (read:
     'intersect ` A ` '). $)
  cint $a class |^| A $.

  ${
    $d x y A $.
    $( Define the intersection of a class.  Definition 7.35 of
       [TakeutiZaring] p. 44. $)
    df-int $a |- |^| A = { x | A. y ( y e. A -> x e. y ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class intersection. $)
    dfint2 $p |- |^| A = { x | A. y e. A x e. y } $=
      ( cint cv wcel wel wi wal cab wral df-int df-ral biabi eqtr4 ) CDBECFABGZ
      HBIZAJPBCKZAJABCLRQAPBCMNO $.
      $([28-Jun-98]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Equality law for intersection. $)
    inteq $p |- ( A = B -> |^| A = |^| B ) $=
      ( vx vy wceq wel wral cab cint raleq biabdv dfint2 3eqtr4g ) ABEZCDFZDAGZ
      CHODBGZCHAIBINPQCODABJKCDALCDBLM $.
      $([13-Sep-99]$)
  $}

  ${
    inteqi.1 $e |- A = B $.
    $( Equality inference for class intersection. $)
    inteqi   $p |- |^| A = |^| B $=
      ( wceq cint inteq ax-mp ) ABDAEBEDCABFG $.
      $([2-Sep-03]$)
  $}

  ${
    inteqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for class intersection. $)
    inteqd   $p |- ( ph -> |^| A = |^| B ) $=
      ( wceq cint inteq syl ) ABCEBFCFEDBCGH $.
      $([2-Sep-03]$)
  $}

  ${
    $d x A y $.  $d x B y $.  $( ` y ` is dummy. $)
    elint.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint   $p |- ( A e. |^| B <-> A. x ( x e. B -> A e. x ) ) $=
      ( vy cv wcel wel wi wal cint wceq eleq1 bilimd bialdv df-int vtoclab ) AF
      ZCGZEAHZIZAJSBRGZIZAJEBCKDEFZBLZUAUCAUETUBSUDBRMNOEACPQ $.
      $([21-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.
    elint2.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint2   $p |- ( A e. |^| B <-> A. x e. B A e. x ) $=
      ( cint wcel cv wi wal wral elint df-ral bitr4 ) BCEFAGZCFBNFZHAIOACJABCDK
      OACLM $.
      $([14-Oct-99]$)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Membership in class intersection, with the sethood requirement expressed
       as an antecedent. $)
    elintg   $p |- ( A e. C -> ( A e. |^| B <-> A. x e. B A e. x ) ) $=
      ( vy cv cint wcel wel wral wb wceq eleq1 biraldv bi2bid visset elint2
      vtoclg ) EFZCGZHZEAIZACJZKBTHZBAFZHZACJZKEBDSBLZUAUDUCUGSBTMUHUBUFACSBUEM
      NOASCEPQR $.
      $([21-Nov-03]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Membership in class intersection. $)
    elinti   $p |- ( A e. |^| B -> ( C e. B -> A e. C ) ) $=
      ( vy vx cint wcel cv wi wal wel wceq eleq1 bilimd bialdv visset elint
      biimp vtoclga eleq2 bi2imd cla4gv pm2.43b syl ) ABFZGDHZBGZAUFGZIZDJZCBGZ
      ACGZIZUGEDKZIZDJZUJEAUEEHZALZUOUIDURUNUHUGUQAUFMNOUQUEGUPDUQBEPQRSUJUKULU
      IUMDCBUFCLUGUKUHULUFCBMUFCATUAUBUCUD $.
      $([14-Oct-99]$)
  $}

  ${
    $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbint.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for intersection. $)
    hbint   $p |- ( y e. |^| A -> A. x y e. |^| A ) $=
      ( vz cv wcel wel wi wal cint ax-17 hbel hbim hbal visset elint bial
      3imtr4 ) EFZCGZBEHZIZEJZUDAJBFZCKGZUFAJUCAEUAUBAABBTCUBALZDMUGNOEUECBPQZU
      FUDAUHRS $.
      $([2-Feb-97]$)
  $}

  ${
    $d A x y $.  $d ph y $.  $( ` y ` is dummy. $)
    inteqab.1 $e |- A e. V $.
    $( Membership in the intersection of an abstraction class. $)
    elintab   $p |- ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) $=
      ( vy cab cint wcel cv wi wal elint hbab1 ax-17 hbim weq eleq1 abid
      syl6bb eleq2 bi2imd cbval bitr ) CABFZGHEIZUDHZCUEHZJZEKACBIZHZJZBKECUDDL
      UHUKEBUFUGBABEMUGBNOUKENEBPZUFAUGUJULUFUIUDHAUEUIUDQABRSUEUICTUAUBUC $.
      $([30-Aug-93]$)

    $( Membership in the intersection of an abstraction class. $)
    elintrab   $p |- ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) $=
      ( cv wcel wa cab cint wi wal crab wral elintab impexp bial bitr df-rab
      inteqi eleq2i df-ral 3bitr4 ) CBFZDGZAHZBIZJZGZUEACUDGZKZKZBLZCABDMZJZGUK
      BDNUIUFUJKZBLUMUFBCEOUPULBUEAUJPQRUOUHCUNUGABDSTUAUKBDUBUC $.
      $([17-Oct-99]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The intersection of the empty set is the universal class.  Exercise 2 of
       [TakeutiZaring] p. 44. $)
    int0 $p |- |^| (/) = V $=
      ( vy vx cv c0 wcel wel wi wal cab weq cint cvv noel pm2.21i ax-gen eqid
      2th biabi df-int df-v 3eqtr4 ) ACZDEZBAFZGZAHZBIBBJZBIDKLUFUGBUFUGUEAUCUD
      UBMNOBPQRBADSBTUA $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( An element of a class includes the intersection of the class.  Exercise
       4 of [TakeutiZaring] p. 44 (with correction), generalized to classes. $)
    intss $p |- ( A e. B -> |^| B (_ A ) $=
      ( vx vy wcel cint cv wel wi wal wceq eleq1 eleq2 bi2imd cla4gv pm2.43a
      visset elint syl5ib ssrdv ) ABEZCBFZAUADGZBEZCDHZIZDJZCGZAEZUHUBEUGUAUIUF
      UAUIIDABUCAKUDUAUEUIUCABLUCAUHMNOPDUHBCQRST $.
      $([18-Nov-95]$)

    $( Subclass of a class intersection.  Theorem 5.11(viii) of [Monk1] p. 52
       and its converse. $)
    ssint $p |- ( A (_ |^| B <-> A. x e. B A (_ x ) $=
      ( vy cint wss cv wcel wral wel dfss3 visset elint2 biral ralcom2 bitr4
      3bitr ) BCEZFDGZRHZDBIDAJZACIZDBIZBAGZFZACIZDBRKTUBDBASCDLMNUCUADBIZACIUF
      UADABCOUEUGACDBUDKNPQ $.
      $([14-Oct-99]$)

    $( Subclass of a least upper bound. $)
    ssintub $p |- A (_ |^| { x e. B | A (_ x } $=
      ( vy cv wss crab cint wral wcel sseq2 elrab pm3.27bd rgen ssint mpbir )
      BBAEZFZACGZHFBDEZFZDSIUADSTSJTCJUARUAATCQTBKLMNDBSOP $.
      $([8-Aug-00]$)

    $( Any member of a class is the smallest of those members that include
       it. $)
    intmin $p |- ( A e. B -> A = |^| { x e. B | A (_ x } ) $=
      ( vy wcel cv wss crab cint wa wceq wel wi wral ssid sseq2 eleq2 bi2imd
      rcla4v mpii comm visset elintrab syl5ib ssrdv ssintub jctil eqss sylibr
      ) BCEZBBAFZGZACHIZGZUMBGZJBUMKUJUOUNUJDUMBUJULDALZMZACNZDFZBEZUSUMEURUJUT
      URUJBBGZUTBOUQVAUTMABCUKBKULVAUPUTUKBBPUKBUSQRSTUAULAUSCDUBUCUDUEABCUFUGB
      UMUHUI $.
      $([13-Aug-02]$)

    $( Intersection of subclasses. $)
    ssintss $p |- ( A (_ B -> |^| B (_ |^| A ) $=
      ( vy vx cv wcel wi wal cint wss wel syl2 19.20ii visset elint 3imtr4g
      19.21aiv dfss2 3imtr4 ) CEZAFZTBFZGZCHZDEZBIZFZUEAIZFZGZDHABJUFUHJUDUJDUD
      UBDCKZGZCHUAUKGZCHUGUIUCULUMCUAUBUKLMCUEBDNZOCUEAUNOPQCABRDUFUHRS $.
      $([14-Oct-99]$)
  $}

  ${
    $d x y A $.
    intmin2.1 $e |- A e. V $.
    $( Any set is the smallest of all sets that include it. $)
    intmin2 $p |- A = |^| { x | A (_ x } $=
      ( cv wss cvv crab cint cab wcel wceq intmin ax-mp wa df-rab visset
      biantrur biabi eqtr4 inteqi eqtr ) BBADZEZAFGZHZUCAIZHBFJBUEKCABFLMUDUFUD
      UBFJZUCNZAIUFUCAFOUCUHAUGUCAPQRSTUA $.
      $([20-Sep-03]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The intersection of a non-empty class exists.  Exercise 5 of
       [TakeutiZaring] p. 44 and its converse. $)
    intex $p |- ( -. A = (/) <-> |^| A e. V ) $=
      ( vx c0 wceq wn cint cvv wcel cv wex nnull wss intss visset ssex syl
      19.23aiv sylbi nvelv inteq int0 syl6eq eleq1d mtbiri con2 impbi ) ACDZEZA
      FZGHZUHBIZAHZBJUJBAKULUJBULUIUKLUJUKAMUIUKBNOPQRUGUJUGUJGGHSUGUIGGUGUICFG
      ACTUAUBUCUDUEUF $.
      $([13-Aug-02]$)
  $}

  ${
    $d x y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( The intersection of a non-empty abstraction class exists. $)
    intexab $p |- ( E. x ph <-> |^| { x | ph } e. V ) $=
      ( wex cab c0 wceq wn cint cvv wcel noel3 intex bitr3 ) ABCABDZEFGNHIJABKN
      LM $.
      $([3-Nov-03]$)
  $}

  $( The intersection of a non-empty restricted abstraction class exists. $)
  intexrab $p |- ( E. x e. A ph <-> |^| { x e. A | ph } e. V ) $=
    ( cv wcel wa wex cab cint cvv wrex crab intexab df-rex df-rab inteqi
    eleq1i 3bitr4 ) BDCEAFZBGSBHZIZJEABCKABCLZIZJESBMABCNUCUAJUBTABCOPQR $.
    $([3-Nov-03]$)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The class intersection of the union of two classes.  Theorem 78 of
       [Suppes] p. 42. $)
    intun $p |- |^| ( A u. B ) = ( |^| A i^i |^| B ) $=
      ( vx vy cun cint cin cv wcel wel wi wal wa 19.26 wo elun birim jaob bitr
      bial visset elint bi2an 3bitr4 elin cleqri ) CABEZFZAFZBFZGZDHZUGIZCDJZKZ
      DLZCHZUIIZUQUJIZMZUQUHIUQUKIULAIZUNKZULBIZUNKZMZDLVBDLZVDDLZMUPUTVBVDDNUO
      VEDUOVAVCOZUNKVEUMVHUNULABPQVAUNVCRSTURVFUSVGDUQACUAZUBDUQBVIUBUCUDDUQUGV
      IUBUQUIUJUEUDUF $.
      $([22-Sep-02]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    intpr.1 $e |- A e. V $.
    intpr.2 $e |- B e. V $.
    $( The intersection of a pair is the intersection of its members.  Theorem
       71 of [Suppes] p. 42. $)
    intpr $p |- |^| { A , B } = ( A i^i B ) $=
      ( vx vy cpr cint cin cv wcel wel wi wal wa wceq 19.26 wo visset elpr
      birim jaob bitr bial clel4 bi2an 3bitr4 elint elin cleqri ) EABGZHZABIZFJ
      ZUKKZEFLZMZFNZEJZAKZUSBKZOZUSULKUSUMKUNAPZUPMZUNBPZUPMZOZFNVDFNZVFFNZOURV
      BVDVFFQUQVGFUQVCVERZUPMVGUOVJUPUNABFSTUAVCUPVEUBUCUDUTVHVAVIFUSACUEFUSBDU
      EUFUGFUSUKESUHUSABUIUGUJ $.
      $([14-Oct-99]$)
  $}

  ${
    intsn.1 $e |- A e. V $.
    $( The intersection of a singleton is its member.  Theorem 70 of [Suppes]
       p. 41. $)
    intsn $p |- |^| { A } = A $=
      ( csn cint cpr cin dfsn2 inteqi intpr inidm 3eqtr ) ACZDAAEZDAAFALMAGHAAB
      BIAJK $.
      $([29-Sep-02]$)

    $( Extract the first member of an ordered pair.  Theorem 73 of [Suppes]
       p. 42.  (See ~ op2nd to extract the second member.) $)
    op1st $p |- |^| |^| <. A , B >. = A $=
      ( cop cint csn cpr cin snex zfpaircl intpr df-op inteqi wss wceq wcel
      pri1 snssi ax-mp df-ss mpbi 3eqtr4 intsn eqtr ) ABDZEZEAFZEAUFUGUGABGZGZE
      UGUHHZUFUGUGUHAIABJKUEUIABLMUGUHNZUGUJOAUHPUKABCQAUHRSUGUHTUAUBMACUCUD $.
      $([25-Nov-03]$)
  $}

  ${
    intunsn.1 $e |- B e. V $.
    $( Theorem joining a singleton to an intersection. $)
    intunsn $p |- |^| ( A u. { B } ) = ( |^| A i^i B ) $=
      ( csn cun cint cin intun intsn ineq2i eqtr ) ABDZEFAFZLFZGMBGALHNBMBCIJK
      $.
      $([29-Sep-02]$)
  $}

