$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 The Axiom of Infinity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( First we prove the equivalence of our version of Infinity with
     some others. $)

  ${
    $d x y z w v u f $.  $( ` w v u f ` are dummy. $)
    inf0.1 $e |- om e. V $.
    $( Our axiom of infinity derived from existence of omega. $)
    inf0   $p |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( vu vv vw vf wel wa wex wi wal weq a13b birand biexdv bi2imd cbvalv
      bilan biex cv csuc wceq copab crfg com cres crn wcel cdm cvv wfun
      funrnex wfn fndm frfnom ax-mp eqeltr fnfun mp2 cleq4 biland bialdv
      bi2and cla4ev c0 cfv frzer visset fnfvrn peano1 mp2an eqeltrr wb fvelrn
      cleq3 frsuc fveq1 id suceq cleqan12rd cbvopabv fvex sucex fvopab eqtr
      syl6eq cleq4d sucid mpbiri syl5bi peano2b mpan sylbi a1i jcad comm imp
      syl 19.23aiv ax-gen mpbir ) BAIZXDBCIZCAIZJZCKZLZBMZJZAKXDEAIZECIZXFJZCKZ
      LZEMZJZAKZXKXRAXJXQXDXIXPBEBENZXDXLXHXOBEAOXTXGXNCXTXEXMXFBECOPQRSTUABUBZ
      FUBZGUBZUCZUDZGFUEZYAUFUGUHZUIZUJZEUBZYHUJZXMCUBZYHUJZJZCKZLZEMZXSXRYIYQJ
      AYHYGUKZULUJYGUMZYHULUJULYGUNYRUGULYGUGUOZYRUGUDUGYGUPYAYFUQZURDUSYTYSUGY
      GUTUVAURVAAUBZYHUDZXDYIXQYQUVBYHYAVBUVCXPYPEUVCXLYKXOYOUVBYHYJVBUVCXNYNCU
      VCXFYMXMUVBYHYLVBVCQRVDVEVFVGYGVHZYAYHYAULUJUVDYAUDYAULYFVIBVJURYTVGUGUJU
      VDYHUJUGVGYGVKUVAVLVMVNYPEYKHUBZUGUJZUVEYGVHZYJUDZJZHKZYOYTYKUVJVOHUGYJYG
      VPUVAURUVIYOHUVIYJUVEUCZYGVHZUJZUVLYHUJZJZYOUVFUVHUVOUVHUVFUVOUVHUVFUVMUV
      NUVHUVGUVLUJZUVMUVFUVGYJUVLVQUVFUVPUVGUVGUCZUJUVFUVLUVQUVGUVFUVLUVGYFVHZU
      VQYAUVEYFVRUVRUVGYLUVBUCZUDZACUEZVHZUVQYFUWAUDUVRUWBUDUVGYFUWAVSYEUVTGFAC
      FCNZGANYBYLYDUVSUWCVTYCUVBWAWBWCURACUVGUVSUVQUVEYGWDZUVGUWDWEUVBUVGWAWFWG
      WHWIUVGUWDWJWKWLUVFUVNLUVHUVFUVKUGUJZUVNUVEWMYTUWEUVNUGUVKYGVKUVAWNWOWPWQ
      WRWSYNUVOCUVLUVKYGWDYLUVLUDXMUVMYMUVNYLUVLYJVBYLUVLYHVQVEVFWTXAWOXBVMXC
      $.
      $([15-Oct-96]$)
  $}

  ${
    $d x y z $.
    inf1.1 $e |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $.
    $( Variation of axiom of infinity. $)
    inf1   $p |- E. x ( -. x = (/) /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( wel wa wex wi wal cv c0 wceq wn noel2 imran 19.22i ax-mp ) BAEZRBCECAEF
      CGHBIZFZAGAJZKLMZSFZAGTUCARUBSUABJNOPDQ $.
      $([14-Oct-96]$)

    $( There exists a non-empty set that is a subset of its union. $)
    inf2   $p |- E. x ( -. x = (/) /\ x (_ U. x ) $=
      ( cv c0 wceq wn cuni wss wa wex wel wi wal wcel df-ss eluni bilim bial
      bitr bilan biex inf1 mpbir ) AEZFGHZUFUFIZJZKZALUGBAMZBCMCAMKCLZNZBOZKZAL
      UJUOAUIUNUGUIUKBEZUHPZNZBOUNBUFUHQURUMBUQULUKCUPUFRSTUAUBUCABCDUDUE $.
      $([28-Oct-96]$)
  $}

  ${
    $d x y z w v u f g $.  $d v u f g F $.  $d v u f g R $.  $d v u f g A $.
    $d v u f g B $.
    inf3lem.3 $e |- F =
         { <. y , z >. | z = { w | ( w e. x /\ ( w i^i x ) (_ y ) } } $.
    inf3lem.4 $e |- R = ( rec ( F , (/) ) |` om ) $.
    inf3lem.1 $e |- A e. V $.
    inf3lem.2 $e |- B e. V $.

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lema  $p |- ( A e. ( F ` B ) <-> ( A e. x /\ ( A i^i x ) (_ B ) ) $=
      ( vf vu vv cfv wcel wel cv cin wss wa cab wceq copab fveq1 weq id sseq2
      biland biabdv ax-17 a13b ineq1 sseq1d bi2and cbvab syl6eq
      cleqan12rd cbvopabv eqtr ax-mp visset zfausab fvopab cleq4i cleq3 abcl
      bitr ) EFHPZQEMARZMSZASZTZFUAZUBZMUCZQEVMQZEVMTZFUAZUBZVJVQEVJFNSZVKVNOSZ
      UAZUBZMUCZUDZONUEZPZVQHWHUDVJWIUDFHWHUFHCSZDARZDSZVMTZBSZUAZUBZDUCZUDZBCU
      EWHIWRWGBCONCNUGZBOUGZWJWBWQWFWSUHWTWQWKWMWCUAZUBZDUCWFWTWPXBDWTWOXAWKWNW
      CWMUIUJUKXBWEDMXBMULWEDULDMUGZWKVKXAWDDMAUMXCWMVNWCWLVLVMUNUOUPUQURUSUTVA
      VBONFWFVQLVOMVMAVCVDWCFUDZWEVPMXDWDVOVKWCFVNUIUJUKVEVAVFVPWAMEKVLEUDZVKVR
      VOVTVLEVMVGXEVNVSFVLEVMUNUOUPVHVI $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lemb $p |- ( R ` (/) ) = (/) $=
      ( c0 cfv crfg com cres wceq fveq1 ax-mp cvv wcel frzer zfnull eqtr ) MGNZ
      MHMOPQZNZMGUGRUFUHRMGUGSJTMUAUBUHMRMUAHUCUDTUE $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lemc $p |- ( A e. om -> ( R ` suc A ) = ( F ` ( R ` A ) ) ) $=
      ( com wcel csuc c0 crfg cres cfv frsuc wceq fveq1 ax-mp fveq2 3eqtr4g )
      EMNEOZHPQMRZSZEUGSZHSZUFGSZEGSZHSZPEHTGUGUAZUKUHUAUFGUGUBJUCULUIUAZUMUJUA
      ULUIHUDUNUOEGUGUBJUCUCUE $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lemd $p |- ( A e. om -> ( R ` A ) (_ x ) $=
      ( vv vu com wcel c0 wceq cfv cv wss wi fveq2 inf3lemb syl6eq sseq1d
      nullss mpbiri a1i wn csuc wex wa cleq3 bi2imd peano2b wel visset
      inf3lemc cleq4d cin fvex inf3lema pm3.26 sylbi syl6bi ssrdv sylbir
      comm 19.23adv nnsuc syl5 anabsi5 exp pm2.61d ) EOPZEQRZEGSZATZUAZVQVTUB
      VPVQVTQVSUAVQVRQVSVQVRQGSQEQGUCABCDEFGHIJKLUDUEUFVSUGUHUIVPVQUJZVTVPWAVTV
      PEMTZUKZRZMULVTVPWAUMVPWDVTMWDVPVTWDVPVTUBWCOPZWCGSZVSUAZUBWDVPWEVTWGEWCO
      UNWDVRWFVSEWCGUCUFUOWEWBOPZWGWBUPWHNWFVSWHNTZWFPWIWBGSZHSZPZNAUQZWHWFWKWI
      ABCDWBFGHIJMURLUSUTWLWMWIVSVAWJUAZUMWMABCDWIWJGHIJNURWBGVBVCWMWNVDVEVFVGV
      HUHVIVJMEVKVLVMVNVO $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem1  $p |- ( A e. om -> ( R ` A ) (_ ( R ` suc A ) ) $=
      ( vv vu cv cfv csuc wss c0 wceq fveq2 sseq1d suceq syl sseq2d bitrd
      weq inf3lemb sseq1i nullss mpbir com wcel wa wi wel cin visset inf3lemc
      cleq4d fvex inf3lema syl6bb peano2b sucex sylbi bi2imd sstr comm
      imland syl5bir imp ssrdv exp finds ) MOZGPZVPQZGPZRZSGPZSQZGPZRZNOZGPZW
      EQZGPZRZWHWGQZGPZRZEGPZEQZGPZRZMNEVPSTZVTWAVSRWDWQVQWAVSVPSGUAUBWQVSWCWAW
      QVRWBTVSWCTVPSUCVRWBGUAUDUEUFMNUGZVTWFVSRWIWRVQWFVSVPWEGUAUBWRVSWHWFWRVRW
      GTVSWHTVPWEUCVRWGGUAUDUEUFVPWGTZVTWHVSRWLWSVQWHVSVPWGGUAUBWSVSWKWHWSVRWJT
      VSWKTVPWGUCVRWJGUAUDUEUFVPETZVTWMVSRWPWTVQWMVSVPEGUAUBWTVSWOWMWTVRWNTVSWO
      TVPEUCVRWNGUAUDUEUFWDSWCRWASWCABCDEEGHIJKKUHUIWCUJUKWEULUMZWIWLXAWIUNMWHW
      KXAWIVPWHUMZVPWKUMZUOZXAXDMAUPZVPAOUQZWFRZUNZXEXFWHRZUNZUOWIXAXBXHXCXJXAX
      BVPWFHPZUMXHXAWHXKVPABCDWEWEGHIJNURZXLUSUTABCDVPWFGHIJMURZWEGVAVBVCXAXCVP
      WHHPZUMXJXAWKXNVPXAWGULUMWKXNTWEVDABCDWGWEGHIJWEXLVEXLUSVFUTABCDVPWHGHIJX
      MWGGVAVBVCVGWIXGXIXEXGWIXIXFWFWHVHVIVJVKVLVMVNVO $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem2  $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( A e. om -> -. ( R ` A ) = x ) ) $=
      ( vv vu vf com wcel cv c0 wceq wn cuni wss wa cfv wi csuc fveq2 cleq1d
      binegd bilimd weq inf3lemb cleq1i cleqcom bitr biimp con3 adantr wel
      wex visset inf3lemc cleq4d cin fvex inf3lema pm3.27 sylbi ssd elin
      syl5ibr syl6bi cleq4 biimparc syl5 com23 exp4a exp3a com34 imp3a
      19.23adv ssel eluni syl6ib imp pm2.43d pm2.16 syl6 wpss dfpss2 pssnel
      sylbir inf3lemd syland exp4b pm2.43a adantld a2d finds comm ) EPQARZST
      ZUAZXBXBUBZUCZUDZEGUEZXBTZUAZXGMRZGUEZXBTZUAZUFXGSGUEZXBTZUAZUFXGNRZGUEZX
      BTZUAZUFXGXRUGZGUEZXBTZUAZUFXGXJUFMNEXKSTZXNXQXGYFXMXPYFXLXOXBXKSGUHUIUJU
      KMNULZXNYAXGYGXMXTYGXLXSXBXKXRGUHUIUJUKXKYBTZXNYEXGYHXMYDYHXLYCXBXKYBGUHU
      IUJUKXKETZXNXJXGYIXMXIYIXLXHXBXKEGUHUIUJUKXDXQXFXPXCXPXCXPSXBTXCXOSXBABCD
      EFGHIJKLUMUNSXBUOUPUQURUSXRPQZXGYAYEYJXFYAYEUFZXDXFYJYKYJXFYJYAYEYJXFUDZX
      SXBUCZYAYEYJYLMAUTZXKXSQZUAZUDZMVAZYEYMYAUDZYLYQYEMYLYNYPYEYLYNYDYOUFZYPY
      EUFYLYNYTYJXFYNYNYTUFZUFYJXFYNUVAYJMOUTZOAUTZUDZOVAZUVAXFYNUDYJUVDUVAOYJU
      VBUVCUVAYJUVBYNUVCYTYJUVBYNUVCYTUFYJUVBYNUDZUVCYDYOYJUVCYDUDZUVFYOYJORZYC
      QZUVFYOUFZUVGYJUVIUVHXSHUEZQZUVJYJYCUVKUVHABCDXRFGHIJNVBZLVCVDUVLXKUVHXBV
      EZQYOUVFUVLUVNXSXKUVLUVCUVNXSUCZUDUVOABCDUVHXSGHIJOVBXRGVFVGUVCUVOVHVIVJX
      KUVHXBVKVLVMYDUVIUVCYCXBUVHVNVOVPVQVRVSVTWAWBXFYNUVEXFYNXKXEQUVEXBXEXKWCO
      XKXBWDWEWFVPVSWFWGYDYOWHWIWAWBYSXSXBWJYRXSXBWKMXSXBWLWMVPABCDXRFGHIJUVMLW
      NWOWPWQWRWSWTXA $.
      $([28-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem4  $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( A e. om -> ( R ` A ) (. ( R ` suc A ) ) ) $=
      ( vv com wcel cv c0 wceq wn cuni wss wa cfv csuc wpss inf3lem1 a1d cdif
      inf3lemd inf3lem2 comm jcad pssdifn0 syl6 cin wex inf3lemc cleq4d wel
      eldifi inssdif0 biimpr im2an visset fvex inf3lema sylibr syl5bir wi
      eldifn adantr a1i cleq4 biimprd iman sylib con2 19.23adv difss ssexi
      zfregwOBS syl5 syld dfpss2 syl6ibr ) ENOZAPZQRSWGWGTUAUBZEGUCZEUDGUCZUEZW
      FWHWIWJUAZWIWJRZSZUBWKWFWHWLWNWFWLWHABCDEFGHIJKLUFUGWFWHWGWIUHZQRSZWNWFWH
      WIWGUAZWIWGRSZUBWPWFWHWQWRWFWQWHABCDEFGHIJKLUIUGWHWFWRABCDEFGHIJKLUJUKULW
      IWGUMUNWFMPZWOOZWSWOUOQRZUBZMUPWNWPWFXBWNMWFXBWSWJOZWSWIOZSZUBZWNWFXBXCXE
      WFXCWSWIHUCZOZXBWFWJXGWSABCDEFGHIJKLUQURXBMAUSZWSWGUOWIUAZUBXHWTXIXAXJWSW
      GWIUTXJXAWSWGWIVAVBVCABCDWSWIGHIJMVDEGVEVFVGVHXBXEVIWFWTXEXAWSWGWIVJVKVLU
      LWMXFWMXCXDVIXFSWMXDXCWIWJWSVMVNXCXDVOVPVQUNVRMWOWOWGAVDWGWIVSVTWAWBWCULW
      IWJWDWEUK $.
      $([29-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem5  $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( ( A e. om /\ B e. A ) -> ( R ` B ) (. ( R ` A ) ) ) $=
      ( vv vu com wcel wa cv c0 wceq wn cuni wss cfv wpss wi csuc con nnont
      onsucssi syl adantr wb fveq2 psseq2 bilimd weq inf3lem4 imp visset
      psstr exp comm syl6 adantrd a2d findsg exp4b peano2b sylan5b com4r
      anabsi7 syld com23 wtr trel word ordtr ordom ax-mp ancoms mpd ) EOPZFEP
      ZQZARZSTUAWFWFUBUCQZFGUDZEGUDZUEZWEFOPZWGWJUFZWCWDWKWLUFWCWKWDWLWCWKWDWLU
      FWCWKQZWDFUGZEUCZWLWCWDWOUFZWKWCEUHPWPEUIFEUJUKULWCWKWOWLUFWMWOWGWKWJWCWN
      OPZWOWGWKWJUFUFUFWKWCWQQWOWGWKWJWGWKQZWHMRZGUDZUEZUFWRWHWNGUDZUEZUFWRWHNR
      ZGUDZUEZUFZWRWHXDUGZGUDZUEZUFZWRWJUFMNEWNWSWNTZXAXCWRXLWTXBTXAXCUMWSWNGUN
      WTXBWHUOUKUPMNUQZXAXFWRXMWTXETXAXFUMWSXDGUNWTXEWHUOUKUPWSXHTZXAXJWRXNWTXI
      TXAXJUMWSXHGUNWTXIWHUOUKUPWSETZXAWJWRXOWTWITXAWJUMWSEGUNWTWIWHUOUKUPWGWKX
      CABCDFFGHIJLLURUSXDOPZWQQXGXKUFZWNXDUCXPXQWQXPWRXFXJXPWGXFXJUFZWKWGXPXRWG
      XPXEXIUEZXRABCDXDFGHIJNUTLURXFXSXJXFXSXJWHXEXIVAVBVCVDVCVEVFULULVGVHFVIVJ
      VKVLVMVBVNUSWDWCWKOVOZWDWCQWKUFOFEVPOVQXTOVRVSVTVTWAWBVC $.
      $([29-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem6  $p |- ( ( -. x = (/) /\ x (_ U. x ) -> R : om -1-1-> P~ x ) $=
      ( vv vu cv c0 wceq wn cuni wss wa com cpw wf cfv weq wi wral wf1 wcel
      wal wel wo wpss visset inf3lem5 dfpss2 pm3.27 sylbi syl6 exp3a imp
      adantarl cleqcom bineg sylib adantarr jaod pm2.03d wb word ordtri2 nnord
      syl2an adantl sylibrd exp 19.21aivv r2al sylibr wfn crn df-f wex fvelrn
      cleq3 inf3lemd fvex sselpw syl5bi comm 19.23aiv syl6bi ssrdv ancli crfg
      cres fneq1 frfnom mpbiri ax-mp mpbir jctil f1fv ) AOZPQRXEXESTUAZUBXEUCZG
      UDZMOZGUEZNOZGUEZQZMNUFZUGZNUBUHMUBUHZUAUBXGGUIXFXPXHXFXIUBUJZXKUBUJZUAZX
      OUGZNUKMUKXPXFXTMNXFXSXOXFXSUAZXMMNULZNMULZUMZRZXNYAYDXMYAYBXMRZYCXFXRYBY
      FUGZXQXFXRYGXFXRYBYFXFXRYBUAXJXLUNZYFABCDXKXIGHIJNUOZMUOZUPYHXJXLTZYFUAYF
      XJXLUQYKYFURUSUTVAVBVCXFXQYCYFUGZXRXFXQYLXFXQYCYFXFXQYCUAXLXJUNZYFABCDXIX
      KGHIJYJYIUPYMXLXJQZRZYFYMXLXJTZYOUAYOXLXJUQYPYOURUSYNXMXLXJVDVEVFUTVAVBVG
      VHVIXSXNYEVJZXFXIVKXKVKYQXQXRXIXKVLXIVMXKVMVNVOVPVQVRXOMNUBUBVSVTXHGUBWAZ
      GWBZXGTZUAZUBXGGWCYRUVAYRYTYRNYSXGYRXKYSUJXQXJXKQZUAZMWDXKXGUJZMUBXKGWEUV
      CUVDMXQUVBUVDUVBXQUVDUVBXJXGUJZUVDXQXJXKXGWFXQXJXETUVEABCDXIFGHIJYJLWGXJX
      EXIGWHWIVFWJWKVBWLWMWNWOGHPWPUBWQZQZYRUVGYRUVFUBWAUBGUVFWRPHWSWTJXAXAXBXC
      MNUBXGGXDVT $.
      $([29-Oct-96]$)

    $( Lemma for our infinity -> standard infinity infinity. $)
    inf3lem7  $p |- ( ( -. x = (/) /\ x (_ U. x ) -> om e. V ) $=
      ( cv c0 wceq wn cuni wss wa com cpw wf1 cvv wcel inf3lem6 ccnv crn cdm
      wf f1f fdm syl dfdm4 syl5eqr cleq3d wfun wi frn visset zfpowcl ssex
      3syl df-rn cleq3i sylib funrnex df-f1 pm3.27 sylbi syl5 pm2.43i mpbid )
      AMZNOPVMVMQRSTVMUAZGUBZTUCUDZABCDEFGHIJKLUEVOGUFZUGZUCUDZVPVOVRTUCVOGUHZT
      VRVOTVNGUIZVTTOTVNGUJZTVNGUKULGUMUNUOVOVSVOVQUPZVSVOVOVQUHZUCUDZWCVSUQVOG
      UGZUCUDZWEVOWAWFVNRWGWBTVNGURWFVNVMAUSUTVAVBWFWDUCGVCVDVEUCVQVFULVOWAWCSW
      CTVNGVGWAWCVHVIVJVKVLUL $.
      $([29-Oct-96]$)
  $}

  ${
    $d x y z w $.  $( ` y z w ` are dummy. $)
    inf3.1 $e |- E. x ( -. x = (/) /\ x (_ U. x ) $.
    $( Our infinity -> standard infinity. $)
    inf3   $p |- om e. V $=
      ( vy vz vw cv c0 wceq wn cuni wss wa wex com cvv wcel wel cin cab copab
      crfg cres cleqid visset inf3lem7 19.23aiv ax-mp ) AFZGHIUHUHJKLZAMNOPZUIU
      JAACDEUHUHDFEAQEFUHRCFKLESHCDTZGUANUBZUKUKUCULUCAUDZUMUEUFBUG $.
      $([29-Oct-96]$)
  $}


$(
         +-------------------------------------------------+
         |         "Cantor's Paradise" begins here!        |
         | (i.e. we start to assume the Axiom of Infinity) |
         +-------------------------------------------------+
$)

  ${
    $d x y z w $.
    $( Standard version of axiom of infinity $)
    inf4 $p |- E. x ( E. y ( y e. x /\ A. z -. z e. y ) /\ A. y ( y e. x ->
          E. z ( z e. x /\ A. w ( w e. z <-> ( w e. y \/ w = y ) ) ) ) ) $=
      ( c0 cv wcel wel csuc wi wal wa wex wn weq wo wb wceq df-clel nulleq
      biran ancom bitr biex cab csn cun df-suc dfun2 elsn bilor biab 3eqtr
      cleq2i cleqab bilim bial bi2an com ax-inf inf2 inf3 cleq4 bi2imd
      bialdv bi2and cla4ev peano1 peano2b biimp ax-gen pm3.2i ax-mp mpbi ) EA
      FZGZBAHZBFZIZVOGZJZBKZLZAMZVQCBHNCKZLZBMZVQCAHZDCHDBHZDBOZPZQDKZLZCMZJZBK
      ZLZAMWCWQAVPWGWBWPVPVRERZVQLZBMWGBEVOSWSWFBWSWEVQLWFWRWEVQCVRTUAWEVQUBUCU
      DUCWAWOBVTWNVQVTCFZVSRZWHLZCMWNCVSVOSXBWMCXBWLWHLWMXAWLWHXAWTWKDUEZRWLVSX
      CWTVSVRVRUFZUGWIDFXDGZPZDUEXCVRUHDVRXDUIXFWKDXEWJWIDVRUJUKULUMUNWKDWTUOUC
      UAWLWHUBUCUDUCUPUQURUDEUSGZVRUSGZVSUSGZJZBKZLZWDWCXLAUSAABCABCUTVAVBVOUSR
      ZVPXGWBXKVOUSEVCXMWAXJBXMVQXHVTXIVOUSVRVCVOUSVSVCVDVEVFVGXGXKVHXJBXHXIVRV
      IVJVKVLVMVN $.
      $([3-Nov-96]$)
  $}

  ${
    $d x y z w $.  $( ` z w ` are dummy. $)
    $( The Axiom of Infinity, using definitions to abbreviate. $)
    zfinf $p |- E. x ( (/) e. x /\ A. y ( y e. x -> suc y e. x ) ) $=
      ( vz vw c0 cv wcel wel csuc wi wal wa wex wn weq wo wb wceq df-clel
      nulleq biran ancom bitr biex cab csn cun df-suc dfun2 elsn bilor biab
      3eqtr cleq2i cleqab bilim bial bi2an inf4 mpbir ) EAFZGZBAHZBFZIZVAGZJZBK
      ZLZAMVCCBHNCKZLZBMZVCCAHZDCHDBHZDBOZPZQDKZLZCMZJZBKZLZAMVIWBAVBVLVHWAVBVD
      ERZVCLZBMVLBEVASWDVKBWDVJVCLVKWCVJVCCVDTUAVJVCUBUCUDUCVGVTBVFVSVCVFCFZVER
      ZVMLZCMVSCVEVASWGVRCWGVQVMLVRWFVQVMWFWEVPDUEZRVQVEWHWEVEVDVDUFZUGVNDFWIGZ
      PZDUEWHVDUHDVDWIUIWKVPDWJVOVNDVDUJUKULUMUNVPDWEUOUCUAVQVMUBUCUDUCUPUQURUD
      ABCDUSUT $.
      $([30-Aug-93]$)
  $}


  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The existence of omega (the class of natural numbers).  Axiom 7 of
       Takeuti and Zaring, p. 43. $)
    omex $p |- om e. V $=
      ( vx vy com cv wss wex cvv wcel visset ssex 19.23aiv c0 wel csuc wi wal
      wa peano5 ax-1 19.20i sylan5 19.22i zfinf ax-mp ) CADZEZAFZCGHZUFUHACUEAI
      JKLUEHZBAMBDZNUEHOZBPZQZAFUGUMUFAUIUJCHZUKOZBPUFULBUERUKUOBUKUNSTUAUBABUC
      UDUD $.
      $([6-Aug-94]$)
  $}

  ${
    $d x y z $.  $( ` z ` is dummy. $)
    $( The class of natural numbers omega can be defined as the smallest
       "inductive set", which is valid provided we assume the Axiom of
       Infinity.  Definition 6.3 of Eisenberg, p. 82.  $)
    dfom2  $p |- om =
                 |^| { x | ( (/) e. x /\ A. y ( y e. x -> suc y e. x ) ) } $=
      ( vz com c0 cv wcel wel csuc wi wal wa cab cint wss peano5 zfnull
      inteqab pm3.26 mpgbir weq a13b suceq cleq3d bi2imd a4b1 adantl a2i
      19.20i visset sucex 3imtr4 a1i ax-gen mp2an omex wceq cleq4 bialdv
      bi2and cla4v sylbi peano1 clded2 peano2 clded3 pm3.2i mpi ssri eqssi )
      DEAFZGZBAHZBFZIZVKGZJZBKZLZAMNZEVTGZCFZDGZWBVTGZWBIZVTGZJZJZCKDVTOCVTPWAV
      SVLJAVSAEQRVLVRSTWHCWGWCVSCAHZJZAKZVSWEVKGZJZAKWDWFWJWMAVSWIWLVRWIWLJZVLV
      QWNBCBCUAZVMWIVPWLBCAUBWOVOWEVKVNWBUCUDUEUFUGUHUIVSAWBCUJZRZVSAWEWBWPUKRU
      LUMUNUOCVTDWDEDGZVNDGZVODGZJZBKZLZWCWDWKXCWCJZWQWJXDADUPVKDUQZVSXCWIWCXEV
      LWRVRXBVKDEURXEVQXABXEVMWSVPWTVKDVNURVKDVOURUEUSUTVKDWBURUEVAVBWRXBVCXABW
      TABHWSLAMZIZDGAVNDVNXFUQVOXGDVNXFUCUDXFAVNDVCVDVEVFUNVGVHVIVJ $.
      $([6-Aug-94]$)
  $}


