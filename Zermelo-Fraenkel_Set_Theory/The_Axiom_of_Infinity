$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 The Axiom of Infinity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( First we prove the equivalence of our version of Infinity with
     some others. $)

  ${
    $d x y z w v u f $.  $( ` w v u f ` are dummy. $)
    inf0.1 $e |- om e. V $.
    $( Our Axiom of Infinity derived from existence of omega.  The proof shows
       that the especially contrived class
       " ` ran ( rec ( { <. w , v >. | v = suc w } , y ) |`` om ) ` " exists,
       is a subset of its union, and contains a given set ` y ` (and thus is
       non-empty).  Thus it provides an example demonstrating that a set ` x `
       exists with the necessary properties demanded by ~ ax-inf . $)
    inf0 $p |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( vu vv vw vf wel wa wex wi wal cv csuc wceq copab crdg com cres crn
      wcel c0 cfv cvv visset frzer ax-mp wfn frfnom peano1 fnfvrn mp2an
      eqeltrr wrex wb fvelrn eleq1 fvex sucid sucex ax-17 hbopab1 hbrdg hbres
      hbfv hbsuc cleqid suceq frsucopab mpan2 eleq2d mpbiri syl5bi peano2b
      mpan sylbi a1i jcad com12 eleq2 bi2and cla4ev syl6 r19.23aiv ax-gen cdm
      wfun fndm eqeltr fnfun funrnex mp2 biland biexdv bi2imd bialdv weq
      birand cbvalv bilan biex mpbir ) BAIZXDBCIZCAIZJZCKZLZBMZJZAKXDEAIZECIZXF
      JZCKZLZEMZJZAKZBNZFNGNZOZPZGFQZXTRZSTZUAZUBZENZYGUBZXMCNZYGUBZJZCKZLZEMZX
      SUCYFUDZXTYGXTUEUBYQXTPBUFXTUEYDUGUHYFSUIZUCSUBYQYGUBXTYDUJZUKSUCYFULUMUN
      YOEYJHNZYFUDZYIPZHSUOZYNYRYJUVCUPYSHSYIYFUQUHUVBYNHSYTSUBZUVBYIYTOZYFUDZU
      BZUVFYGUBZJZYNUVBUVDUVIUVBUVDUVGUVHUVBUVAUVFUBZUVGUVDUVAYIUVFURUVDUVJUVAU
      VAOZUBUVAYTYFUSZUTUVDUVFUVKUVAUVDUVKUEUBUVFUVKPUVAUVLVAGFCXTYTYBUVKUEYFCB
      IGVBZCHIGVBZGCUVAGCYTYFGCYESGCXTYDYCGFCVCUVMVDYKSUBGVBVEUVNVFVGYFVHYAUVAV
      IVJVKVLVMVNUVDUVHLUVBUVDUVESUBZUVHYTVOYRUVOUVHYSSUVEYFULVPVQVRVSVTYMUVICU
      VFUVEYFUSYKUVFPXMUVGYLUVHYKUVFYIWAYKUVFYGURWBWCWDWEVQWFXRYHYPJAYGYFWGZUEU
      BYFWHZYGUEUBUVPSUEYRUVPSPYSSYFWIUHDWJYRUVQYSSYFWKUHUEYFWLWMANZYGPZXDYHXQY
      PUVRYGXTWAUVSXPYOEUVSXLYJXOYNUVRYGYIWAUVSXNYMCUVSXFYLXMUVRYGYKWAWNWOWPWQW
      BWCUMXKXRAXJXQXDXIXPBEBEWRZXDXLXHXOXTYIUVRURUVTXGXNCUVTXEXMXFXTYIYKURWSWO
      WPWTXAXBXC $.
      $( [15-Oct-96] $)
  $}

  ${
    $d x y z $.
    inf1.1 $e |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $.
    $( Variation of Axiom of Infinity (using ~ axinf as a hypothesis).  Axiom
       of Infinity of [FreydScedrov] p. 283.  $)
    inf1 $p |- E. x ( -. x = (/) /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( wel wa wex wi wal cv c0 wceq wn noel2 imran 19.22i ax-mp ) BAEZRBCECAEF
      CGHBIZFZAGAJZKLMZSFZAGDTUCARUBSUABJNOPQ $.
      $( [14-Oct-96] $)

    $( Variation of Axiom of Infinity. There exists a non-empty set that is a
       subset of its union (using ~ axinf as a hypothesis). $)
    inf2 $p |- E. x ( -. x = (/) /\ x (_ U. x ) $=
      ( cv c0 wceq wn cuni wss wa wex wel wi wal inf1 wcel dfss2 eluni bilim
      bial bitr bilan biex mpbir ) AEZFGHZUFUFIZJZKZALUGBAMZBCMCAMKCLZNZBOZKZAL
      ABCDPUJUOAUIUNUGUIUKBEZUHQZNZBOUNBUFUHRURUMBUQULUKCUPUFSTUAUBUCUDUE $.
      $( [28-Oct-96] $)
  $}

  ${
    $d x y z w v u f g $.  $d v u f g G $.  $d v u f g F $.  $d v u f g A $.
    $d v u f g B $.
    inf3lem.1 $e |- G =
         { <. y , z >. | z = { w e. x | ( w i^i x ) (_ y } } $.
    inf3lem.2 $e |- F = ( rec ( G , (/) ) |` om ) $.
    inf3lem.3 $e |- A e. V $.
    inf3lem.4 $e |- B e. V $.

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lema $p |- ( A e. ( G ` B ) <-> ( A e. x /\ ( A i^i x ) (_ B ) ) $=
      ( vf vu vv cfv wcel cv cin wss crab wa wceq copab weq id sseq2 birabsdv
      ineq1 sseq1d cbvrabv syl6eq cleqan12rd cbvopabv eqtr fveq1i visset rabex
      fvopab eleq2i elrab bitr ) EFHPZQEMRZARZSZFTZMVEUAZQEVEQEVESZFTZUBVCVHEVC
      FNRZVFORZTZMVEUAZUCZONUDZPVHFHVPHCRZDRZVESZBRZTZDVEUAZUCZBCUDVPIWCVOBCONC
      NUEZBOUEZVQVKWBVNWDUFWEWBVSVLTZDVEUAVNWEWAWFDVEVTVLVSUGUHWFVMDMVEDMUEVSVF
      VLVRVDVEUIUJUKULUMUNUOUPONFVNVHLVGMVEAUQURVLFUCVMVGMVEVLFVFUGUHUSUOUTVGVJ
      MEVEVDEUCVFVIFVDEVEUIUJVAVB $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemb $p |- ( F ` (/) ) = (/) $=
      ( c0 cfv crdg com cres fveq1i cvv wcel wceq 0ex frzer ax-mp eqtr ) MGNMHM
      OPQZNZMMGUFJRMSTUGMUAUBMSHUCUDUE $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemc $p |- ( A e. om -> ( F ` suc A ) = ( G ` ( F ` A ) ) ) $=
      ( com wcel csuc c0 crdg cres cfv frsuc fveq1i fveq2i 3eqtr4g ) EMNEOZHPQM
      RZSEUESZHSUDGSEGSZHSPEHTUDGUEJUAUGUFHEGUEJUAUBUC $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemd $p |- ( A e. om -> ( F ` A ) (_ x ) $=
      ( vv vu com wcel c0 wceq cfv cv wss wn wa csuc wrex nnsuc fveq2 sseq1d
      wel visset inf3lemc eleq2d cin fvex inf3lema pm3.26bd syl6bi ssrdv
      syl5bir com12 r19.23aiv syl exp 0ss inf3lemb syl6eq mpbiri pm2.61d2 ) EOP
      ZEQRZEGSZATZUAZVIVJUBZVMVIVNUCEMTZUDZRZMOUEVMMEUFVQVMMOVQVOOPZVMVQVMVPGSZ
      VLUAVRVQVKVSVLEVPGUGUHVRNVSVLVRNTZVSPVTVOGSZHSZPZNAUIZVRVSWBVTABCDVOFGHIJ
      MUJLUKULWCWDVTVLUMWAUAABCDVTWAGHIJNUJVOGUNUOUPUQURUSUTVAVBVCVJVMQVLUAVLVD
      VJVKQVLVJVKQGSQEQGUGABCDEFGHIJKLVEVFUHVGVH $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem1 $p |- ( A e. om -> ( F ` A ) (_ ( F ` suc A ) ) $=
      ( vv vu cv cfv csuc wss c0 wceq fveq2 suceq fveq2d sseq12d weq inf3lemb
      0ss eqsstr com wcel wa wi wel cin visset inf3lemc eleq2d fvex inf3lema
      syl6bb peano2b sucex sylbi bi2imd sstr com12 imland syl5bir imp ssrdv
      exp finds ) MOZGPZVMQZGPZRSGPZSQZGPZRNOZGPZVTQZGPZRZWCWBQZGPZRZEGPZEQZGPZ
      RMNEVMSTZVNVQVPVSVMSGUAWKVOVRGVMSUBUCUDMNUEZVNWAVPWCVMVTGUAWLVOWBGVMVTUBU
      CUDVMWBTZVNWCVPWFVMWBGUAWMVOWEGVMWBUBUCUDVMETZVNWHVPWJVMEGUAWNVOWIGVMEUBU
      CUDVQSVSABCDEEGHIJKKUFVSUGUHVTUIUJZWDWGWOWDUKMWCWFWOWDVMWCUJZVMWFUJZULZWO
      WRMAUMZVMAOUNZWARZUKZWSWTWCRZUKZULWDWOWPXBWQXDWOWPVMWAHPZUJXBWOWCXEVMABCD
      VTVTGHIJNUOZXFUPUQABCDVMWAGHIJMUOZVTGURUSUTWOWQVMWCHPZUJXDWOWFXHVMWOWBUIU
      JWFXHTVTVAABCDWBVTGHIJVTXFVBXFUPVCUQABCDVMWCGHIJXGWBGURUSUTVDWDXAXCWSXAWD
      XCWTWAWCVEVFVGVHVIVJVKVL $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem2 $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( A e. om -> -. ( F ` A ) = x ) ) $=
      ( vv vu vf com wcel cv c0 wceq wn cuni wss wa cfv wi csuc fveq2 cleq1d
      binegd bilimd weq inf3lemb cleq1i cleqcom bitr biimp con3i adantr wel
      wex ssel eluni syl6ib visset inf3lemc eleq2d cin fvex inf3lema pm3.27bd
      sseld elin syl5ibr syl6bi eleq2 biimparc syl5 com23 exp4a exp3a com34
      imp3a 19.23adv sylan9r pm2.43d con3 syl6 wpss dfpss2 pssnel sylbir
      inf3lemd syland exp4b pm2.43a adantld a2d finds com12 ) EPQARZSTZUAZXAXAU
      BZUCZUDZEGUEZXATZUAZXFMRZGUEZXATZUAZUFXFSGUEZXATZUAZUFXFNRZGUEZXATZUAZUFX
      FXQUGZGUEZXATZUAZUFXFXIUFMNEXJSTZXMXPXFYEXLXOYEXKXNXAXJSGUHUIUJUKMNULZXMX
      TXFYFXLXSYFXKXRXAXJXQGUHUIUJUKXJYATZXMYDXFYGXLYCYGXKYBXAXJYAGUHUIUJUKXJET
      ZXMXIXFYHXLXHYHXKXGXAXJEGUHUIUJUKXCXPXEXOXBXOXBXOSXATXBXNSXAABCDEFGHIJKLU
      MUNSXAUOUPUQURUSXQPQZXFXTYDYIXEXTYDUFZXCXEYIYJYIXEYIXTYDYIXEUDZXRXAUCZXTY
      DYIYKMAUTZXJXRQZUAZUDZMVAZYDYLXTUDZYKYPYDMYKYMYOYDYKYMYCYNUFZYOYDUFYKYMYS
      XEYMMOUTZOAUTZUDZOVAZYIYMYSUFZXEYMXJXDQUVCXAXDXJVBOXJXAVCVDYIUVBUVDOYIYTU
      VAUVDYIYTYMUVAYSYIYTYMUVAYSUFYIYTYMUDZUVAYCYNYIUVAYCUDZUVEYNYIORZYBQZUVEY
      NUFZUVFYIUVHUVGXRHUEZQZUVIYIYBUVJUVGABCDXQFGHIJNVEZLVFVGUVKXJUVGXAVHZQYNU
      VEUVKUVMXRXJUVKUVAUVMXRUCABCDUVGXRGHIJOVEXQGVIVJVKVLXJUVGXAVMVNVOYCUVHUVA
      YBXAUVGVPVQVRVSVTWAWBWCWDWEWFYCYNWGWHWCWDYRXRXAWIYQXRXAWJMXRXAWKWLVRABCDX
      QFGHIJUVLLWMWNWOWPWQWRWSWT $.
      $( [28-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description.  In the proof, we invoke the Axiom of
       Regularity in the form of ~ zfreg . $)
    inf3lem3 $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( A e. om -> -. ( F ` A ) = ( F ` suc A ) ) ) $=
      ( vv com wcel cv c0 wceq wn cuni wss wa cfv csuc cdif inf3lemd a1d
      inf3lem2 com12 jcad pssdifn0 syl6 cin wrex inf3lemc eleq2d wel eldifi
      inssdif0 biimpr im2an visset fvex inf3lema sylibr syl5bir wi eldifn
      adantr a1i eleq2 biimprd iman sylib con2i exp3a r19.23adv difss ssexi
      zfreg syl5 syld ) ENOZAPZQRSWDWDTUAUBZEGUCZEUDGUCZRZSZWCWEWDWFUEZQRSZWIWC
      WEWFWDUAZWFWDRSZUBWKWCWEWLWMWCWLWEABCDEFGHIJKLUFUGWEWCWMABCDEFGHIJKLUHUIU
      JWFWDUKULWCMPZWJUMQRZMWJUNWIWKWCWOWIMWJWCWNWJOZWOWIWCWPWOUBZWNWGOZWNWFOZS
      ZUBZWIWCWQWRWTWCWRWNWFHUCZOZWQWCWGXBWNABCDEFGHIJKLUOUPWQMAUQZWNWDUMWFUAZU
      BXCWPXDWOXEWNWDWFURXEWOWNWDWFUSUTVAABCDWNWFGHIJMVBEGVCVDVEVFWQWTVGWCWPWTW
      OWNWDWFVHVIVJUJWHXAWHWRWSVGXASWHWSWRWFWGWNVKVLWRWSVMVNVOULVPVQMWJWJWDAVBW
      DWFVRVSVTWAWBUI $.
      $( [29-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem4 $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( A e. om -> ( F ` A ) (. ( F ` suc A ) ) ) $=
      ( cv c0 wceq wn cuni wss wa com wcel cfv csuc wpss wi inf3lem1 a1i
      inf3lem3 jcad dfpss2 syl6ibr ) AMZNOPULULQRSZETUAZEGUBZEUCGUBZRZUOUPOPZSU
      OUPUDUMUNUQURUNUQUEUMABCDEFGHIJKLUFUGABCDEFGHIJKLUHUIUOUPUJUK $.
      $( [29-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem5 $p |- ( ( -. x = (/) /\ x (_ U. x ) ->
                    ( ( A e. om /\ B e. A ) -> ( F ` B ) (. ( F ` A ) ) ) $=
      ( vv vu com wcel wa cv c0 wceq wn cuni wss cfv wpss wi elnn ancoms csuc
      word nnord ordsucss syl adantr fveq2 psseq2d bilimd weq peano2b inf3lem4
      com12 sylbir visset psstr exp syl6 a2d ad2antll findsg sylan5b syld
      com23 imp mpd ) EOPZFEPZQZARZSTUAVRVRUBUCQZFGUDZEGUDZUEZVQFOPZVSWBUFZVPVO
      WCFEUGUHVOVPWCWDUFVOWCVPWDVOWCVPWDUFVOWCQVPFUIZEUCZWDVOVPWFUFZWCVOEUJWGEU
      KFEULUMUNVOWEOPZWFWDUFWCVOWHQWFWDVSVTMRZGUDZUEZUFVSVTWEGUDZUEZUFZVSVTNRZG
      UDZUEZUFZVSVTWOUIZGUDZUEZUFZWDMNEWEWIWETZWKWMVSXCWJWLVTWIWEGUOUPUQMNURZWK
      WQVSXDWJWPVTWIWOGUOUPUQWIWSTZWKXAVSXEWJWTVTWIWSGUOUPUQWIETZWKWBVSXFWJWAVT
      WIEGUOUPUQWHWCWNFUSZVSWCWMABCDFFGHIJLLUTVAVBWOOPZWRXBUFWHWEWOUCXHVSWQXAVS
      XHWQXAUFZVSXHWPWTUEZXIABCDWOFGHIJNVCLUTWQXJXAWQXJXAVTWPWTVDVEVAVFVAVGVHVI
      VEXGVJVKVEVLVMVNVA $.
      $( [29-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem6 $p |- ( ( -. x = (/) /\ x (_ U. x ) -> F : om -1-1-> P~ x ) $=
      ( vv vu cv c0 wceq wn cuni wss wa com cpw wf cfv weq wi wral wf1 wcel
      wal wel wo wpss visset inf3lem5 dfpss2 pm3.27bd syl6 exp3a imp adantrl
      cleqcom bineg sylib adantrr jaod con2d wb word ordtri3 nnord syl2an
      adantl sylibrd exp 19.21aivv r2al sylibr wfn crn crdg cres frfnom fneq1
      mpbiri ax-mp wrex fvelrn eleq1 inf3lemd fvex elpw syl5bi com12 r19.23aiv
      syl6bi ssrdv ancli df-f mpbir jctil f1fv ) AOZPQRXDXDSTUAZUBXDUCZGUDZMOZG
      UEZNOZGUEZQZMNUFZUGZNUBUHMUBUHZUAUBXFGUIXEXOXGXEXHUBUJZXJUBUJZUAZXNUGZNUK
      MUKXOXEXSMNXEXRXNXEXRUAZXLMNULZNMULZUMZRZXMXTYCXLXTYAXLRZYBXEXQYAYEUGZXPX
      EXQYFXEXQYAYEXEXQYAUAXIXKUNZYEABCDXJXHGHIJNUOZMUOZUPYGXIXKTYEXIXKUQURUSUT
      VAVBXEXPYBYEUGZXQXEXPYJXEXPYBYEXEXPYBUAXKXIUNZYEABCDXHXJGHIJYIYHUPYKXKXIQ
      ZRZYEYKXKXITYMXKXIUQURYLXLXKXIVCVDVEUSUTVAVFVGVHXRXMYDVIZXEXHVJXJVJYNXPXQ
      XHXJVKXHVLXJVLVMVNVOVPVQXNMNUBUBVRVSXGGUBVTZGWAZXFTZUAZYOYRGHPWBUBWCZQZYO
      JYTYOYSUBVTPHWDUBGYSWEWFWGYOYQYONYPXFYOXJYPUJXIXJQZMUBWHXJXFUJZMUBXJGWIUV
      AUVBMUBUVAXPUVBUVAXIXFUJZUVBXPXIXJXFWJXPXIXDTUVCABCDXHFGHIJYILWKXIXDXHGWL
      WMVSWNWOWPWQWRWSWGUBXFGWTXAXBMNUBXFGXCVS $.
      $( [29-Oct-96] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description.  In the proof, we invoke the Axiom of
       Replacement in the form of ~ funrnex . $)
    inf3lem7 $p |- ( ( -. x = (/) /\ x (_ U. x ) -> om e. V ) $=
      ( cv c0 wceq wn cuni wss wa com cpw wf1 cvv wcel inf3lem6 ccnv crn cdm
      wfun funrnex wf f1f frn visset pwex ssex 3syl df-rn eleq1i sylib df-f1
      pm3.27bd syl13 fdm syl dfdm4 syl5eqr eleq1d mpbid ) AMZNOPVJVJQRSTVJUAZGU
      BZTUCUDZABCDEFGHIJKLUEVLGUFZUGZUCUDZVMVNUHZUCUDZVNUIZVPVLUCVNUJVLGUGZUCUD
      ZVRVLTVKGUKZVTVKRWATVKGULZTVKGUMVTVKVJAUNUOUPUQVTVQUCGURUSUTVLWBVSTVKGVAV
      BVCVLVOTUCVLGUHZTVOVLWBWDTOWCTVKGVDVEGVFVGVHVIVE $.
      $( [29-Oct-96] $)
  $}

  ${
    $d x y z w $.  $( ` y z w ` are dummy. $)
    inf3.1 $e |- E. x ( -. x = (/) /\ x (_ U. x ) $.
    $( Note: the special <PRE> tag is handled by mmwtex.c. $)
    $( Our Axiom of Infinity ~ ax-inf implies the standard Axiom of Infinity.
       The hypothesis is a variant of our Axiom of Infinity provided by
       ~ inf2 , and the conclusion is the version of the Axiom of Infinity
       shown as Axiom 7 in [TakeutiZaring] p. 43.  (Other standard versions are
       proved later as ~ inf4 and ~ zfinf .)   The main proof is provided by
       ~ inf3lema through ~ inf3lem7 , and this final piece eliminates the
       auxiliary hypothesis of ~ inf3lem7 .  This proof is due to
       Ian Sutherland, Richard Heck, and Norman Megill and was posted
       on Usenet as shown below.  Although the result is not new, the authors
       were unable to find a published proof.
<PRE>
(As posted to sci.logic on 30-Oct-96, with annotations added.)

Theorem:  The statement "There exists a non-empty set that is a subset
of its union" implies the Axiom of Infinity.

Proof:  Let X be a nonempty set which is a subset of its union; the latter
property is equivalent to saying that for any y in X, there exists a z in X
such that y is in z.

Define by finite recursion a function F:omega-->(power X) such that
  F_0 = 0  (See ~ inf3lemb .)
  F_n+1 = {y<X | y^X subset F_n}  (See ~ inf3lemc .)
Note: ^ means intersect, < means \in ("element of").
(Finite recursion as typically done requires the existence of omega;
to avoid this we can just use transfinite recursion restricted to omega.
F is a class-term that is not necessarily a set at this point.)

Lemma 1.  F_n subset F_n+1.  (See ~ inf3lem1 .)
Proof:  By induction:  F_0 subset F_1.  If y < F_n+1, then y^X subset F_n,
so if F_n subset F_n+1, then y^X subset F_n+1, so y < F_n+2.

Lemma 2.  F_n =/= X.  (See ~ inf3lem2 .)
Proof:  By induction:  F_0 =/= X because X is not empty.  Assume F_n =/= X.
Then there is a y in X that is not in F_n.  By definition of X, there is a
z in X that contains y.  Suppose F_n+1 = X.  Then z is in F_n+1, and z^X
contains y, so z^X is not a subset of F_n, contrary to the definition of
F_n+1.

Lemma 3.  F_n =/= F_n+1.  (See ~ inf3lem3 .)
Proof:  Using the identity y^X subset F_n <-> y^(X-F_n) = 0, we have
F_n+1 = {y<X | y^(X-F_n) = 0}.  Let q = {y<X-F_n | y^(X-F_n) = 0}.
Then q subset F_n+1.  Since X-F_n is not empty by Lemma 2 and q is the
set of \in-minimal elements of X-F_n, by Foundation q is not empty, so q
and therefore F_n+1 have an element not in F_n.

Lemma 4.  F_n proper_subset F_n+1.  (See ~ inf3lem4 .)
Proof:  Lemmas 1 and 3.

Lemma 5.  F_m proper_subset F_n, m < n.  (See ~ inf3lem5 .)
Proof:  Fix m and use induction on n > m.  Basis: F_m proper_subset F_m+1
by Lemma 4.  Induction:  Assume F_m proper_subset F_n.  Then since F_n
proper_subset F_n+1, F_m proper_subset F_n+1 by transitivity of proper
subset.

By Lemma 5, F_m =/= F_n for m =/= n, so F is 1-1.  (See ~ inf3lem6 .)
Thus the inverse of F is a function with range omega and domain a subset
of power X, so omega exists by Replacement.  (See ~ inf3lem7 .)
Q.E.D.
</PRE> $)
    inf3 $p |- om e. V $=
      ( vy vz vw cv c0 wceq wn cuni wss wa wex com cvv wcel cin crab copab
      crdg cres cleqid visset inf3lem7 19.23aiv ax-mp ) AFZGHIUGUGJKLZAMNOPZBUH
      UIAACDEUGUGDFEFUGQCFKEUGRHCDSZGTNUAZUJUJUBUKUBAUCZULUDUEUF $.
      $( [29-Oct-96] $)
  $}

  ${
    $d x y z w $.
    $( The statement "there exists a set that is the proper subset of its
       union" is equivalent to the Axiom of Infinity (shown on the right-hand
       side in the form of ~ omex .)  This provides us with a very short way to
       express of the Axiom of Infinity using only simple defined symbols.  The
       proof does not depend on the Axiom of Infinity.. $)
    inf5 $p |- ( E. x x (. U. x <-> om e. V ) $=
      ( vy vz vw cv cuni wpss wex com cvv wcel c0 wceq wn wss wa dfpss2
      pm3.27bd unieq uni0 syl6req cleqtr mpand nsyl pssss jca 19.22i cin crab
      copab crdg cres cleqid visset inf3lem7 19.23aiv syl csn cdif difexg 0ex
      snid disj4 disj3 bitr3 peano1 eleq2 mpbii eldif sylib pm3.27d sylbi a3i
      ax-mp unidif0 wlim limom limuni eqtr4 psseq2i mpbir psseq1 psseq2d bitrd
      cla4egv mpi impbi ) AEZWHFZGZAHZIJKZWKWHLMZNZWHWIOZPZAHWLWJWPAWJWNWOWJWHW
      IMZWMWJWOWQNWHWIQRWMLWIMWQWMWILFLWHLSTUAWHLWIUBUCUDWHWIUEUFUGWPWLAABCDWHW
      HCEDEWHUHBEODWHUIMBCUJZLUKIULZWRWRUMWSUMAUNZWTUOUPUQWLILURZUSZJKZWKIXAJUT
      XCXBXBFZGZWKXEXBIGZLXAKZXFLVAVBXFXGXFNZIXBMZXGNZXHIXAUHLMXIIXAVCIXAVDVEXI
      LIKZXJXILXBKZXKXJPXIXKXLVFIXBLVGVHLIXAVIVJVKVLVMVNXDIXBXDIFZIIVOIVPIXMMVQ
      IVRVNVSVTWAWJXEAXBJWHXBMZWJXBWIGXEWHXBWIWBXNWIXDXBWHXBSWCWDWEWFUQWG $.
      $( [24-Mar-04] $) $( [23-Mar-04] $)
  $}


$(
         +-------------------------------------------------+
         |         "Cantor's Paradise" begins here!        |
         | (i.e. we start to assume the Axiom of Infinity) |
         +-------------------------------------------------+
$)

  ${
    $d x y z w $.
    $( A standard version of axiom of infinity, expanded to primitives.  In
       English, it says:  there exists a set that contains the empty set and
       the successors of all of its members.  See ~ zfinf to see it converted
       to abbreviations. $)
    inf4 $p |- E. x ( E. y ( y e. x /\ A. z -. z e. y ) /\ A. y ( y e. x ->
          E. z ( z e. x /\ A. w ( w e. z <-> ( w e. y \/ w = y ) ) ) ) ) $=
      ( c0 cv wcel wel csuc wi wal wa wex wn weq wo wb com peano1 peano2
      ax-gen axinf inf2 inf3 wceq eleq2 bi2imd bialdv bi2and cla4ev mp2an wrex
      0el df-rex bitr sucel bilim bial bi2an biex mpbi ) EAFZGZBAHZBFZIZVBGZJZB
      KZLZAMZVDCBHNCKZLBMZVDCAHDCHDBHDBOPQDKZLCMZJZBKZLZAMERGZVERGZVFRGZJZBKZVK
      SWBBVETUAVJVSWCLARAABCABCUBUCUDVBRUEZVCVSVIWCVBREUFWDVHWBBWDVDVTVGWAVBRVE
      UFVBRVFUFUGUHUIUJUKVJVRAVCVMVIVQVCVLBVBULVMBCVBUMVLBVBUNUOVHVPBVGVOVDVGVN
      CVBULVOCDVEVBUPVNCVBUNUOUQURUSUTVA $.
      $( [3-Nov-96] $)
  $}

  ${
    $d x y z w $.
    $( A standard version of the axiom of infinity, using definitions to
       abbreviate. $)
    zfinf $p |- E. x ( (/) e. x /\ A. y e. x suc y e. x ) $=
      ( vz vw c0 cv wcel csuc wral wa wex wel wn wal weq wo wb wi inf4 wrex
      0el df-rex bitr sucel biral df-ral bi2an biex mpbir ) EAFZGZBFZHUJGZBUJIZ
      JZAKBALZCBLMCNZJBKZUPCALDCLDBLDBOPQDNZJCKZRBNZJZAKABCDSUOVBAUKURUNVAUKUQB
      UJTURBCUJUAUQBUJUBUCUNUTBUJIVAUMUTBUJUMUSCUJTUTCDULUJUDUSCUJUBUCUEUTBUJUF
      UCUGUHUI $.
      $( [4-Mar-04] $) $( [30-Aug-93] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The existence of omega (the class of natural numbers).  Axiom 7 of
       [TakeutiZaring] p. 43.  This theorem is proved assuming the Axiom of
       Infinity and in fact is equivalent to it.  A finitist (someone who
       doesn't believe in infinity) could, without contradiction, omit the
       axiom of Infinity and instead deny it; this would lead to ` om = On `
       (the proper class of ordinals) by ~ omon and ~ onprc .  The finitist
       could still develop natural number, integer, and rational number
       arithmetic but would be denied the real numbers (as well as much of the
       rest of mathematics).  In deference to the finitist, much of our
       development is done, when possible, without invoking the Axiom of
       Infinity; an example is Peano's axioms ~ peano1 through ~ peano5
       (which many textbooks prove more easily assuming Infinity).
       The mathematics used by computers is essentially finitist; for example,
       computers cannot work directly with real numbers but only
       approximations of them in the form of floating-point numbers. $)
    omex $p |- om e. V $=
      ( vx vy com cv wss wex cvv wcel c0 csuc wral wa zfinf wel wi peano5 ax-1
      r19.20i2 sylan5 19.22i ax-mp visset ssex 19.23aiv ) CADZEZAFZCGHZIUEHZBDZ
      JUEHZBUEKZLZAFUGABMUMUFAUIBANUKOZBCKUFULBUEPUKUNBUECUNUJCHQRSTUAUFUHACUEA
      UBUCUDUA $.
      $( [6-Aug-94] $)
  $}

  $( Omega is an ordinal number. $)
  omelon $p |- om e. On $=
    ( com con0 wcel wceq cvv omex onprc eleq1 mtbiri mt2 omon ori mt3 ) ABCZABD
    ZOAECZFOPBECGABEHIJNOKLM $.
    $( [10-May-98] $)

  ${
    $d x y z $.
    $( The class of natural numbers omega can be defined as the smallest
       "inductive set," which is valid provided we assume the Axiom of
       Infinity.  Definition 6.3 of [Eisenberg] p. 82.  $)
    dfom2 $p |- om = |^| { x | ( (/) e. x /\ A. y e. x suc y e. x ) } $=
      ( vz com c0 cv wcel csuc wral wa cab cint wi wss 0ex elintab pm3.26
      mpgbir wel wal weq suceq eleq1d rcla4v adantl a2i 19.20i visset sucex
      3imtr4 a1i rgen peano5 mp2an peano1 peano2 pm3.2i omex wceq eleq2 raleqd
      bi2and bi2imd cla4v sylbi mpi ssriv eqssi ) DEAFZGZBFZHZVIGZBVIIZJZAKLZEV
      PGZCFZVPGZVRHZVPGZMZCDIDVPNVQVOVJMAVOAEOPVJVNQRWBCDWBVRDGZVOCASZMZATZVOVT
      VIGZMZATVSWAWEWHAVOWDWGVNWDWGMVJVMWGBVRVIBCUAVLVTVIVKVRUBUCUDUEUFUGVOAVRC
      UHZPZVOAVTVRWIUIPUJUKULCVPUMUNCVPDVSEDGZVLDGZBDIZJZWCWKWMUOWLBDVKUPULUQVS
      WFWNWCMZWJWEWOADURVIDUSZVOWNWDWCWPVJWKVNWMVIDEUTVMWLBVIDVIDVLUTVAVBVIDVRU
      TVCVDVEVFVGVH $.
      $( [27-Jun-04] $) $( [6-Aug-94] $)
  $}

  ${
    $d A x $.
    $( A simplification of ~ elom assuming the Axiom of Infinity.  $)
    elom3 $p |- ( A e. om <-> A. x ( Lim x -> A e. x ) ) $=
      ( com wcel cv wlim wi wal word wa elomg ibi pm3.27d limom omex wceq
      limeq eleq2 bi2imd cla4v mpi impbi ) BCDZAEZFZBUDDZGZAHZUCBIZUHUCUIUHJABC
      KLMUHCFZUCNUGUJUCGACOUDCPUEUJUFUCUDCQUDCBRSTUAUB $.
      $( [30-May-03] $)
  $}

  ${
    $d x y $.
    $( A simplification of ~ df-om assuming the Axiom of Infinity.  $)
    dfom3 $p |- om = { x | A. y ( Lim y -> x e. y ) } $=
      ( cv wlim wel wi wal com elom3 biabri ) BCDABEFBGAHBACIJ $.
      $( [30-May-03] $)
  $}

  ${
    $d x A $.
    $( A set is strictly dominated by the class of natural numbers iff it is
       finite.  Theorem 42 of [Suppes] p. 151.  This theorem provides two
       equivalent ways to express " ` A ` is finite".  The Axiom of Infinity
       is used for the reverse implication. $)
    isfinite $p |- ( A ~< om <-> E. x e. om A ~~ x ) $=
      ( com csdm wbr cv cen wrex isfinite2 cdom wn wa isfinite1 cvv wcel wi
      omex ensymg ax-mp con3i imlan brsdom sylibr syl impbi ) BCDEZBAFGEACHZABI
      UGBCJEZCBGEZKZLZUFABMUKUHBCGEZKZLUFUJUMUHULUICNOULUIPQBCNRSTUABCUBUCUDUE
      $.
      $( [19-Jul-04] $) $( [24-Apr-04] $)
  $}

  $( A natural number is strictly dominated by the set of natural numbers. $)
  nnsdom $p |- ( A e. om -> A ~< om ) $=
    ( com wcel cdom wbr cen wn wa csdm omsdomnn cvv wi omex ensymg ax-mp con3i
    imlan syl brsdom sylibr ) ABCZABDEZABFEZGZHZABIEUAUBBAFEZGZHUEAJUGUDUBUCUFB
    KCUCUFLMABKNOPQRABST $.
    $( [13-Nov-03] $) $( [28-Oct-03] $)

  ${
    $d x y $.
    $( Omega is equinumerous to a proper subset of itself.  Example 13.2(4) of
       [Eisenberg] p. 216. $)
    omenps $p |- om ~~ ( om \ { (/) } ) $=
      ( com cvv wcel c0 csn cdif cen wbr omex limom limenpsi ax-mp ) ABCAADEFGH
      IABJKL $.
      $( [30-Jul-03] $)
  $}

  $( The set of natural numbers is equinumerous to its successor. $)
  omensuc $p |- om ~~ suc om $=
    ( com cvv wcel csuc cen wbr omex limom limensuci ax-mp ) ABCAADEFGABHIJ $.
    $( [18-Nov-03] $) $( [30-Oct-03] $)

  ${
    $d x y A $.
    $( Any infinite ordinal is equinumerous to its successor.  Exercise 7 of
       [TakeutiZaring] p. 88. $)
    infensuc $p |- ( ( A e. On /\ om (_ A ) -> A ~~ suc A ) $=
      ( vx vy con0 wcel com wss csuc cen wbr omelon cv wceq id suceq breq12d
      weq omensuc a1i wi csn cvv visset sucex en2sn mp2an wa cin c0 cun unen
      df-suc 3brtr4g exp wel wn word eloni ordeirr syl disjsn sylibr sucelon
      3imtr4 jca syl5 mpan2 com12 ad2antll wlim wral limensuc mpan a1d tfindsg
      mpan12 ) ADEFDEZFAGAAHZIJZKBLZVTHZIJZFFHZIJZCLZWEHZIJZWFWFHZIJZVSBCAFVTFM
      ZVTFWAWCIWJNVTFOPBCQZVTWEWAWFIWKNVTWEOPVTWFMZVTWFWAWHIWLNVTWFOPVTAMZVTAWA
      VRIWMNVTAOPWDVQRSWEDEZWGWITVQFWEGZWGWNWIWGWEUAZWFUAZIJZWNWITWEUBEWFUBEWRC
      UCZWEWSUDWEWFUBUBUEUFWGWRUGZWEWPUHUIMZWFWQUHUIMZUGZWIWNWTXCWIWTXCUGWEWPUJ
      WFWQUJWFWHIWEWFWPWQUKWEULWFULUMUNWNXAXBWNCCUOUPZXAWNWEUQXDWEURWEUSUTWEWEV
      AVBWFDEZWFWFEUPZWNXBXEWFUQXFWFURWFUSUTWEVCWFWFVAVDVEVFVGVHVIVTVJZVQUGFVTG
      ZUGWBWOWGTCVTVKXGWBVQXHVTUBEXGWBBUCVTUBVLVMVIVNVOVP $.
      $( [19-Nov-03] $) $( [30-Oct-03] $)
  $}

