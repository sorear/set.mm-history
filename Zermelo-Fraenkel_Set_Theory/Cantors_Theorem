$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cantor's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y F $.  $( ` x y ` are dummy. $)
    canth.1 $e |- A e. V $.
    $( No set ` A ` is equinumerous to its power set (Cantor's theorem), i.e.
       no function can map ` A ` it onto its power set.  Compare Theorem 6B(b)
       of [Enderton] p. 132.  For the equinumerosity version, see ~ canth2 .
       Note that ` A ` must be a set:  this theorem does not hold when ` A ` is
       too large to be a set; see ~ ncanth for a counterexample. $)
    canth $p |- -. F : A -onto-> P~ A $=
      ( vx vy cpw wfo crn wceq forn wf cv cfv wcel wn crab fof wral wb weq id
      fveq2 eleq12d binegd elrab baibr nbbn eleq2 con3i sylbi syl rgen wrex
      wfn wi ffn fvelrn biimpd con3d ralnex syl5ib mpi wss ssrab rabex elpw
      mpbir mpbiri 3syl pm2.65i ) AAFZBGZBHZVKIZAVKBJVLAVKBKZDLZVPBMZNZOZDAPZVM
      NZOZVNOAVKBQVOELZBMZVTIZOZEARZWBWFEAWCANZWCWDNZOZWCVTNZSZWFWKWHWJVSWJDWCA
      DETZVRWIWMVPWCVQWDWMUAVPWCBUBUCUDUEUFWLWIWKSZOWFWIWKUGWEWNWDVTWCUHUIUJUKU
      LVOWEEAUMZOWBWGVOWAWOVOBAUNZWAWOUOAVKBUPWPWAWOEAVTBUQURUKUSWEEAUTVAVBVNWA
      VNWAVTVKNZWQVTAVCVSDAVDVTAVSDACVEVFVGVMVKVTUHVHUIVIVJ $.
      $( [7-Aug-94] $)
  $}

  $( Cantor's theorem fails for the universal class (which is not a set but a
     proper class by ~ nvelv ).  Specifically, the identity function maps the
     universe onto its power class.  Compare ~ canth that works for sets.  See
     also the remark in ~ ru about NF, in which Cantor's theorem fails for sets
     that are "too large."  This theorem gives some intuition behind that
     failure, since in NF the universal class is a set.  $)
  ncanth $p |- I : V -onto-> P~ V $=
    ( cvv cpw cid wf1o wfo f1ovi wceq wb pwv f1oeq3 ax-mp mpbir f1ofo ) AABZCDZ
    ANCEOAACDZFNAGOPHINAACJKLANCMK $.
    $( [29-Jun-04] $) $( [29-Jun-04] $)

