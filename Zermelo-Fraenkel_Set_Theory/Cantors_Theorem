$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cantor's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y F $.  $( ` x y ` are dummy. $)
    canth.1 $e |- A e. V $.
    $( No set is equinumerous to its power set (Cantor's theorem).  Compare
       Theorem 6B(b) of Enderton, p. 132. $)
    canth   $p |- -. F : A -onto-> P~ A $=
      ( vx vy cpw wfo crn wceq forn wf cv wcel cfv wn wa cab fof wi wal wex
      wfn ffn fvelrn biimpd syl pm2.16d alnex syl5ib wb visset weq eleq1 id
      fveq2 eleq12d binegd bi2and abcl baibr nbbn eleq2 con3 sylbi ax-gen mpi
      wss zfausab sselpw ssab mpbi mpbiri 3syl pm2.65i ) AAFZBGZBHZVOIZAVOBJVPA
      VOBKZDLZAMZVTVTBNZMZOZPZDQZVQMZOZVROAVOBRVSELZAMZWIBNZWFIZOZSZETZWHVSWJWL
      PEUAZOWHWOVSWGWPVSBAUBZWGWPSAVOBUCWQWGWPEAWFBUDUEUFUGWJWLEUHUIWNEWJWIWKMZ
      OZWIWFMZUJZWMWTWJWSWEWJWSPDWIEUKDEULZWAWJWDWSVTWIAUMXBWCWRXBVTWIWBWKXBUNV
      TWIBUOUPUQURUSUTXAWRWTUJZOWMWRWTVAWLXCWKWFWIVBVCVDUFVEVFVRWGVRWGWFVOMZVQV
      OWFVBWFAVGXDWFAWDDACVHVIWDDAVJVKVLVCVMVN $.
      $([7-Aug-94]$)
  $}


