$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Indexed union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend class notation to include indexed union. $)
  ciun $a class U. x e. A B $.

  $( Extend class notation to include indexed intersection. $)
  ciin $a class |^| x e. A B $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define indexed union.  Ordinarily, ` A ` would be independent of ` x `
       and ` B ` would depend on ` x ` .  In this case ` x ` is called an index,
       ` A ` is called an index set, and ` B ` is called an indexed set.  In
       most books, ` x e. A ` is written as a subscript or underneath the
       ` U. ` .  Definition of [Stoll] p. 45.  $)
    df-iun $a |- U. x e. A B = { y | E. x e. A y e. B } $.

    $( Define indexed intersection.  Definition of [Stoll] p. 45.  See the
       remarks for its sibling operation of indexed union ~ df-iun . $)
    df-iin $a |- |^| x e. A B = { y | A. x e. A y e. B } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.
    $( Membership in indexed union. $)
    eliun $p |- ( A e. U. x e. B C <-> E. x e. B A e. C ) $=
      ( vy ciun wcel cvv wrex elisset wi cv a1i r19.23aiv wceq eleq1 birexdv
      df-iun elab2g pm5.21nii ) BACDFZGBHGZBDGZACIZBUAJUCUBACUCUBKALCGBDJMNELZD
      GZACIUDEBUAHUEBOUFUCACUEBDPQAECDRST $.
      $( [3-Sep-03] $)

    $( Membership in indexed intersection. $)
    eliin $p |- ( A e. D -> ( A e. |^| x e. B C <-> A. x e. B A e. C ) ) $=
      ( vy cv wcel wral ciin wceq eleq1 biraldv df-iin elab2g ) FGZDHZACIBDHZAC
      IFBACDJEPBKQRACPBDLMAFCDNO $.
      $( [3-Sep-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Indexed union of a constant class, i.e. where ` B ` does not depend
       on ` x ` . $)
    iunconst $p |- ( -. A = (/) -> U. x e. A B = B ) $=
      ( vy c0 wceq wn ciun cv wcel wex wa wb n0 ibar sylbi wrex eliun df-rex
      19.41v 3bitr syl6rbbr cleqrd ) BEFGZDABCHZCUDDIZCJZAIBJZAKZUGLZUFUEJZUDUI
      UGUJMABNUIUGOPUKUGABQUHUGLAKUJAUFBCRUGABSUHUGATUAUBUC $.
      $( [7-Sep-04] $) $( [5-Sep-04] $)
  $}

  ${
    $d x y $.  $d y z A $.  $d x z B $.  $d z C $.
    $( Law combining indexed union with indexed intersection. $)
    iuniin $p |- U. x e. A |^| y e. B C (_ |^| y e. B U. x e. A C $=
      ( vz ciin ciun cv wcel wral wrex r19.12 eliun cvv wb visset eliin ax-mp
      birex bitr biral 3imtr4 ssriv ) FACBDEGZHZBDACEHZGZFIZEJZBDKZACLZUJACLZBD
      KZUIUFJZUIUHJZUJABCDMUOUIUEJZACLULAUICUENUQUKACUIOJZUQUKPFQZBUIDEORSTUAUP
      UIUGJZBDKZUNURUPVAPUSBUIDUGORSUTUMBDAUICENUBUAUCUD $.
      $( [21-Aug-04] $) $( [17-Aug-04] $)
  $}

  ${
    $d y z A $.  $d x y z B $.  $d z ph $.
    $( The indexed union of a restricted abstraction class. $)
    iunrab $p |- U. x e. A { y e. B | ph } = { y e. B | E. x e. A ph } $=
      ( vz wsb wrex crab cv wcel wa cab ciun df-rab ax-17 hbs1 hbrex weq
      sbequ12 birexdv cbvrab eliun elrabsf birex r19.42v 3bitr biabri 3eqtr4r
      ) ACFGZBDHZFEIFJZEKZUKLZFMABDHZCEIBDACEIZNZUKFEOUOUKCFBEBJZEKZCPZUSFPUOFP
      UJCBDURDKCPACFQRCFSAUJBDACFTUAUBUNFUQULUQKULUPKZBDHUMUJLZBDHUNBULDUPUCVAV
      BBDACBULEUTUDUEUMUJBDUFUGUHUI $.
      $( [24-Jan-04] $) $( [3-Jan-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Equality theorem for indexed union. $)
    iuneq1 $p |- ( A = B -> U. x e. A C = U. x e. B C ) $=
      ( vy wceq cv wcel wrex cab ciun rexeq biabdv df-iun 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [27-Jun-98] $)

    $( Equality theorem for restricted existential quantifier. $)
    iineq1 $p |- ( A = B -> |^| x e. A C = |^| x e. B C ) $=
      ( vy wceq cv wcel wral cab ciin raleq biabdv df-iin 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [27-Jun-98] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Subclass theorem for indexed union. $)
    ss2iun $p |- ( A. x e. A B (_ C -> U. x e. A B (_ U. x e. A C ) $=
      ( vy wss wral ciun cv wcel wrex hbra1 wi ra4 ssel syl6 r19.22d eliun
      3imtr4g ssrdv ) CDFZABGZEABCHZABDHZUBEIZCJZABKUEDJZABKUEUCJUEUDJUBUFUGABU
      AABLUBAIBJUAUFUGMUAABNCDUEOPQAUEBCRAUEBDRST $.
      $( [30-Nov-03] $) $( [26-Nov-03] $)

    $( Equality theorem for indexed union. $)
    iuneq2 $p |- ( A. x e. A B = C -> U. x e. A B = U. x e. A C ) $=
      ( wss wral wa ciun wceq ss2iun im2an eqss biral r19.26 bitr 3imtr4 ) CDEZ
      ABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQSGZABFU
      AUFUHABCDLMQSABNOUBUCLP $.
      $( [3-Nov-03] $) $( [22-Oct-03] $)

    $( Equality theorem for indexed intersection. $)
    iineq2 $p |- ( A. x e. A B = C -> |^| x e. A B = |^| x e. A C ) $=
      ( vy wceq wral cv wcel cab ciin hbra1 wb ra4 eleq2 syl6 imp biralda
      biabdv df-iin 3eqtr4g ) CDFZABGZEHZCIZABGZEJUDDIZABGZEJABCKABDKUCUFUHEUCU
      EUGABUBABLUCAHBIZUEUGMZUCUIUBUJUBABNCDUDOPQRSAEBCTAEBDTUA $.
      $( [4-Nov-03] $) $( [22-Oct-03] $)
  $}

  ${
    iuneq2i.1 $e |- ( x e. A -> B = C ) $.
    $( Equality inference for indexed union. $)
    iuneq2i $p |- U. x e. A B = U. x e. A C $=
      ( wceq wral ciun rgen iuneq2 ax-mp ) CDFZABGABCHABDHFLABEIABCDJK $.
      $( [3-Nov-03] $) $( [22-Oct-03] $)

    $( Equality inference for indexed intersection. $)
    iineq2i $p |- |^| x e. A B = |^| x e. A C $=
      ( wceq wral ciin rgen iineq2 ax-mp ) CDFZABGABCHABDHFLABEIABCDJK $.
      $( [4-Nov-03] $) $( [22-Oct-03] $)
  $}

  ${
    $d x ph $.
    iuneq2dv.1 $e |- ( ph -> ( x e. A -> B = C ) ) $.
    $( Equality deduction for indexed union. $)
    iuneq2dv $p |- ( ph -> U. x e. A B = U. x e. A C ) $=
      ( wceq wral ciun r19.21aiv iuneq2 syl ) ADEGZBCHBCDIBCEIGAMBCFJBCDEKL $.
      $( [5-Aug-04] $) $( [3-Aug-04] $)

    $( Equality deduction for indexed intersection. $)
    iineq2dv $p |- ( ph -> |^| x e. A B = |^| x e. A C ) $=
      ( wceq wral ciin r19.21aiv iineq2 syl ) ADEGZBCHBCDIBCEIGAMBCFJBCDEKL $.
      $( [5-Aug-04] $) $( [3-Aug-04] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed union. $)
    hbiu1 $p |- ( y e. U. x e. A B -> A. x y e. U. x e. A B ) $=
      ( vz cv wcel wrex cab wal ciun hbre1 hbab df-iun eleq2i bial 3imtr4 ) BFZ
      EFDGZACHZEIZGZUBAJRACDKZGZUDAJTAEBSACLMUCUARAECDNOZUDUBAUEPQ $.
      $( [14-Oct-03] $) $( [12-Oct-03] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed intersection. $)
    hbii1 $p |- ( y e. |^| x e. A B -> A. x y e. |^| x e. A B ) $=
      ( vz cv wcel wral cab wal ciin hbra1 hbab df-iin eleq2i bial 3imtr4 ) BFZ
      EFDGZACHZEIZGZUBAJRACDKZGZUDAJTAEBSACLMUCUARAECDNOZUDUBAUEPQ $.
      $( [16-Oct-03] $) $( [15-Oct-03] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $( ` z w ` are dummy. $)
    dfiun2.1 $e |- B e. V $.
    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44. $)
    dfiun2 $p |- U. x e. A B = U. { y | E. x e. A y = B } $=
      ( vw vz cv wcel wrex cab wel wceq wa wex ciun cuni df-rex clel3 exancom
      bitr bilan 19.42v bitr4 biex excom an12 visset weq cleq1 birexdv elab
      3bitr4 3bitr biabi df-iun df-uni 3eqtr4 ) FHZDIZACJZFKFGLZGHZBHZDMZACJZBK
      ZIZNZGOZFKACDPVGQVAVJFVAAHCIZUTNZAOZVKVBVCDMZNZNZAOZGOZVJUTACRVMVPGOZAOVR
      VLVSAVLVKVOGOZNVSUTVTVKUTVNVBNGOVTGUSDESVNVBGTUAUBVKVOGUCUDUEVPAGUFUAVQVI
      GVBVKVNNZNZAOVBWAAOZNVQVIVBWAAUCVPWBAVKVBVNUGUEVHWCVBVHVNACJZWCVFWDBVCGUH
      BGUIVEVNACVDVCDUJUKULVNACRUAUBUMUEUNUOAFCDUPFGVGUQUR $.
      $( [27-Jun-98] $)

    $( Alternate definition of indexed intersection when ` B ` is a set.
       Definition 15(b) of [Suppes] p. 44. $)
    dfiin2 $p |- |^| x e. A B = |^| { y | E. x e. A y = B } $=
      ( vw vz cv wcel wral cab wceq wrex wel wi wal ciin cint df-ral clel4
      bilim 19.21v bitr4 bial alcom bitr wa wex impexp 19.23v bitr3 visset weq
      cleq1 birexdv elab df-rex birim 3bitr biabi df-iin df-int 3eqtr4 ) FHZDIZ
      ACJZFKGHZBHZDLZACMZBKZIZFGNZOZGPZFKACDQVKRVFVOFVFAHCIZVEOZAPZVPVGDLZVMOZO
      ZAPZGPZVOVEACSVRWAGPZAPWCVQWDAVQVPVTGPZOWDVEWEVPGVDDETUAVPVTGUBUCUDWAAGUE
      UFWBVNGWBVPVSUGZAUHZVMOZVNWBWFVMOZAPWHWIWAAVPVSVMUIUDWFVMAUJUKVLWGVMVLVSA
      CMZWGVJWJBVGGULBGUMVIVSACVHVGDUNUOUPVSACUQUFURUCUDUSUTAFCDVAFGVKVBVC $.
      $( [28-Jun-98] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.
    cbviunv.1 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis. $)
    cbviunv $p |- U. x e. A B = U. y e. A C $=
      ( vz cv wcel wrex cab ciun weq eleq2d cbvrexv biabi df-iun 3eqtr4 ) GHZDI
      ZACJZGKSEIZBCJZGKACDLBCELUAUCGTUBABCABMDESFNOPAGCDQBGCEQR $.
      $( [15-Sep-03] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed union. $)
    iunss $p |- ( U. x e. A B (_ C <-> A. x e. A B (_ C ) $=
      ( vy wss wral cv wcel wa wi wal ciun dfss2 biral df-ral impexp bial
      19.21v bitr2 3bitr wex 19.23v wrex eliun df-rex bitr birim bitr4 alcom
      3bitr4 ) CDFZABGZAHBIZEHZCIZJZUODIZKZELZALZABCMZDFZUMUPURKZELZABGUNVEKZAL
      VAULVEABECDNOVEABPVFUTAUTUNVDKZELVFUSVGEUNUPURQRUNVDESTRUAUSALZELUOVBIZUR
      KZELVAVCVHVJEVHUQAUBZURKVJUQURAUCVIVKURVIUPABUDVKAUOBCUEUPABUFUGUHUIRUSAE
      UJEVBDNUKT $.
      $( [13-Sep-03] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed union. $)
    ssiun $p |- ( E. x e. A C (_ B ->  C (_ U. x e. A B ) $=
      ( vy wss wrex cv wcel wa wex ciun df-rex wi wal pm3.35 imlan exp32 com23
      imp ssel sylan5 19.22i 19.21aiv eliun bitr2 bilim bial 19.37v dfss2
      3bitr4 sylib sylbi ) DCFZABGAHBIZUNJZAKZDABCLZFZUNABMUQEHZDIZUOUTCIZJZNZA
      KZEOZUSUQVEEUPVDAUOVAVBNZVDUNUOVGVDUOVAVGVCUOVAVGVCVAVGJVBUOVAVBPQRSTDCUT
      UAUBUCUDVAVCAKZNZEOVAUTURIZNZEOVFUSVIVKEVHVJVAVJVBABGVHAUTBCUEVBABMUFUGUH
      VEVIEVAVCAUIUHEDURUJUKULUM $.
      $( [3-Sep-03] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Identity law for subset of an indexed union. $)
    ssiun2 $p |- ( x e. A ->  B (_ U. x e. A B ) $=
      ( vy cv wcel ciun wa wrex ra4e eliun sylibr exp ssrdv ) AEBFZDCABCGZODEZC
      FZQPFZORHRABISRABJAQBCKLMN $.
      $( [14-Oct-03] $) $( [12-Oct-03] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    ssiun2s.1 $e |- ( x = C -> B = D ) $.
    $( Subset relationship for an indexed union. $)
    ssiun2s $p |- ( C e. A ->  D (_ U. x e. A B ) $=
      ( vy wcel ciun wss cv wi ax-17 hbiu1 hbss hbim wceq eleq1 sseq1d bi2imd
      ssiun2 vtoclgf pm2.43i ) DBHZEABCIZJZAKZBHZCUEJZLUDUFLAGDBGKZDHAMUDUFAUDA
      MAGEUEUJEHAMAGBCNOPUGDQZUHUDUIUFUGDBRUKCEUEFSTABCUAUBUC $.
      $( [2-Nov-03] $) $( [26-Oct-03] $)
  $}

  ${
    $d x y C $.  $d x y A $.  $d y B $.
    $( Subset theorem for an indexed intersection. $)
    ssiin $p |- ( C (_ |^| x e. A B <-> A. x e. A C (_ B ) $=
      ( vy cv wcel ciin wi wal wss wral cvv wb visset eliin ax-mp bilim
      r19.21v bitr4 df-ral bitr bial dfss2 biral 19.21v alcom bitr3 3bitr
      3bitr4 ) EFZDGZUKABCHZGZIZEJAFBGZULUKCGZIZIZAJZEJZDUMKDCKZABLZUOUTEUOURAB
      LZUTUOULUQABLZIVDUNVEULUKMGUNVENEOAUKBCMPQRULUQABSTURABUAUBUCEDUMUDVCUREJ
      ZABLUPVFIZAJZVAVBVFABEDCUDUEVFABUAVHUSEJZAJVAVIVGAUPUREUFUCUSAEUGUHUIUJ
      $.
      $( [20-Oct-03] $) $( [15-Oct-03] $)
  $}

  ${
    $d x y C $.  $d x y A $.  $d y B $.
    $( Subset implication for an indexed intersection. $)
    iinss $p |- ( E. x e. A B (_ C ->  |^| x e. A B (_ C ) $=
      ( vy cv wcel wi wal wrex ciin wss wa wex 19.12 df-rex 19.28v biex bitr4
      bial 3imtr4 wral r19.36av cvv wb visset eliin ax-mp syl5ib 19.20i syl
      dfss2 birex ) EFZCGZUNDGZHZEIZABJZUNABCKZGZUPHZEIZCDLZABJUTDLUSUQABJZEIZV
      CAFBGZUQMZEIZANZVHANZEIUSVFVHAEOUSVGURMZANVJURABPVIVLAVGUQEQRSVEVKEUQABPT
      UAVEVBEVEUOABUBZUPVAUOUPABUCUNUDGVAVMUEEUFAUNBCUDUGUHUIUJUKVDURABECDULUME
      UTDULUA $.
      $( [22-Oct-03] $) $( [15-Oct-03] $)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( Class union in terms of indexed union.  Definition of [Stoll] p. 43. $)
    uniiun $p |- U. A = U. x e. A x $=
      ( vy cuni wel wrex cab cv ciun dfuni2 df-iun eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $( [28-Jun-98] $)

    $( Class intersection in terms of indexed intersection.  Definition of
       [Stoll] p. 44. $)
    intiin $p |- |^| A = |^| x e. A x $=
      ( vy cint wel wral cab cv ciin dfint2 df-iin eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $( [28-Jun-98] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( An indexed  union of the empty set is empty. $)
    iun0 $p |- U. x e. A (/) = (/) $=
      ( vy c0 ciun cv wcel wrex eliun wn wb noel a1i nrex pm5.21 mp2an bitr
      cleqri ) CABDEZDCFZSGTDGZABHZUAATBDIUBJUAJZUBUAKUAABUCAFBGTLZMNUDUBUAOPQR
      $.
      $( [27-Mar-03] $) $( [26-Mar-03] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( There is a non-empty class in an indexed collection ` B ( x ) ` iff the
       indexed  union of them is non-empty. $)
    iunn0 $p |- ( E. x e. A -. B = (/) <-> -. U. x e. A B = (/) ) $=
      ( vy c0 wceq wn wrex cv wcel wex wa ciun n0 birex df-rex excom exdistr
      eliun bitr2 biex 3bitr3 bitr4 3bitr ) CEFGZABHDIZCJZDKZABHAIBJZUHLAKZABCM
      ZEFGZUEUHABDCNOUHABPUJUFUKJZDKZULUIUGLZDKAKUOAKZDKUJUNUOADQUIUGADRUPUMDUM
      UGABHUPAUFBCSUGABPTUAUBDUKNUCUD $.
      $( [19-Oct-03] $) $( [15-Oct-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Indexed union of intersection.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iunin $p |- U. x e. A ( B i^i C ) = ( B i^i U. x e. A C ) $=
      ( vy cin ciun cv wcel wrex wa r19.42v elin birex eliun bilan 3bitr4
      cleqri ) EABCDFZGZCABDGZFZEHZSIZABJZUCCIZUCUAIZKZUCTIUCUBIUFUCDIZKZABJUFU
      IABJZKUEUHUFUIABLUDUJABUCCDMNUGUKUFAUCBDOPQAUCBSOUCCUAMQR $.
      $( [30-Mar-04] $) $( [26-Mar-04] $)

    $( Indexed intersection of union.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ intiin to recover
       Enderton's theorem. $)
    iinun $p |- |^| x e. A ( B u. C ) = ( B u. |^| x e. A C ) $=
      ( vy cun ciin cv wcel wral wo r19.32v elun biral cvv wb visset eliin
      ax-mp bilor 3bitr4 cleqri ) EABCDFZGZCABDGZFZEHZUCIZABJZUGCIZUGUEIZKZUGUD
      IZUGUFIUJUGDIZKZABJUJUNABJZKUIULUJUNABLUHUOABUGCDMNUKUPUJUGOIZUKUPPEQZAUG
      BDORSTUAUQUMUIPURAUGBUCORSUGCUEMUAUB $.
      $( [3-Sep-04] $) $( [19-Aug-04] $)

    $( Indexed union of class difference.  Generalization of half of theorem
       "De Morgan's laws" in [Enderton] p. 31.  Use ~ intiin to recover
       Enderton's theorem. $)
    iundif $p |- U. x e. A ( B \ C ) = ( B \ |^| x e. A C ) $=
      ( vy cdif ciun ciin cv wcel wrex wn wa r19.42v eldif birex wral cvv wb
      visset eliin ax-mp bineg rexnal bitr4 bilan 3bitr4 eliun cleqri ) EABCDFZ
      GZCABDHZFZEIZUJJZABKZUNCJZUNULJZLZMZUNUKJUNUMJUQUNDJZLZMZABKUQVBABKZMUPUT
      UQVBABNUOVCABUNCDOPUSVDUQUSVAABQZLVDURVEUNRJURVESETAUNBDRUAUBUCVAABUDUEUF
      UGAUNBUJUHUNCULOUGUI $.
      $( [?] $) $( [19-Aug-04] $)

    $( Indexed intersection of class difference.  Generalization of half of
       theorem "De Morgan's laws" in [Enderton] p. 31.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iindif $p |- ( -. A = (/) ->
                 |^| x e. A ( B \ C ) = ( B \ U. x e. A C ) ) $=
      ( vy c0 wceq wn cdif ciin ciun cv wcel wral wa r19.28zv eldif biral wrex
      eliun bineg ralnex bitr4 bilan 3bitr4g cvv wb visset eliin ax-mp cleqrd
      ) BFGHZEABCDIZJZCABDKZIZULELZUMMZABNZUQCMZUQUOMZHZOZUQUNMZUQUPMULUTUQDMZH
      ZOZABNUTVFABNZOUSVCUTVFABPURVGABUQCDQRVBVHUTVBVEABSZHVHVAVIAUQBDTUAVEABUB
      UCUDUEUQUFMVDUSUGEUHAUQBUMUFUIUJUQCUOQUEUK $.
      $( [?] $) $( [19-Aug-04] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.
    iunxsn.1 $e |- A e. V $.
    iunxsn.2 $e |- ( x = A -> B = C ) $.
    $( A singleton index picks out an instance of an indexed union's
       argument. $)
    iunxsn $p |- U. x e. { A } B = C $=
      ( vy csn ciun cv wcel wrex eliun wa wex df-rex wceq elsn biran biex
      eleq2d ceqsexv bitr cleqri ) GABHZCIZDGJZUFKUGCKZAUELZUGDKZAUGUECMUIAJZUE
      KZUHNZAOZUJUHAUEPUNUKBQZUHNZAOUJUMUPAULUOUHABRSTUHUJABEUOCDUGFUAUBUCUCUCU
      D $.
      $( [27-Mar-04] $) $( [26-Mar-04] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y C $.  $d x y $.
    $( Separate a union in the index of an indexed union. $)
    iunxun $p |- U. x e. ( A u. B ) C = ( U. x e. A C u. U. x e. B C ) $=
      ( vy cun ciun cv wcel wrex wo wa wex df-rex elun biran andir bitr biex
      19.43 eliun bi2or bitr4 3bitr 3bitr4 cleqri ) EABCFZDGZABDGZACDGZFZEHZDIZ
      AUGJZULUIIZULUJIZKZULUHIULUKIUNAHZUGIZUMLZAMURBIZUMLZURCIZUMLZKZAMZUQUMAU
      GNUTVEAUTVAVCKZUMLVEUSVGUMURBCOPVAVCUMQRSVFVBAMZVDAMZKUQVBVDATUOVHUPVIUOU
      MABJVHAULBDUAUMABNRUPUMACJVIAULCDUAUMACNRUBUCUDAULUGDUAULUIUJOUEUF $.
      $( [30-Mar-04] $) $( [26-Mar-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A relationship involving union and indexed intersection.  Exercise 23 of
       [Enderton] p. 33. $)
    iinuni $p |- ( A u. |^| B ) = |^| x e. B ( A u. x ) $=
      ( vy cint cun cv ciin wcel wo wral wel r19.32v elun biral visset elint2
      bilor 3bitr4r cvv wb eliin ax-mp 3bitr4 cleqri ) DBCEZFZACBAGZFZHZDGZBIZU
      KUFIZJZUKUIIZACKZUKUGIUKUJIZULDALZJZACKULURACKZJUPUNULURACMUOUSACUKBUHNOU
      MUTULAUKCDPZQRSUKBUFNUKTIUQUPUAVAAUKCUITUBUCUDUE $.
      $( [27-Nov-03] $) $( [25-Nov-03] $)

    $( A relationship involving union and indexed union.  Exercise 25 of
      [Enderton] p. 33. $)
    iununi $p |- ( ( B = (/) -> A = (/) ) <->
                ( A u. U. B ) = U. x e. B ( A u. x ) ) $=
      ( vy c0 wceq wi cuni cun cv ciun wcel wel wrex wo wn wb imor r19.45zv
      noel2 con2i biorf birexdv bitr3d syl jaoi bicomd sylbi elun birex
      syl6bbr eluni2 bilor bitr eliun 3bitr4g cleqrd wex wa eleq2 eluni ax-17
      19.45 3bitr4 df-rex 3bitr3g biimpd 19.39 orc pm3.26 syl34 19.22i 3syl
      19.37v sylib 19.23adv n0 3imtr4g a3d impbi ) CEFZBEFZGZBCHZIZACBAJZIZKZFZ
      WCDWEWHWCDJZBLZDAMZACNZOZWJWGLZACNZWJWELZWJWHLZWCWNWKWLOZACNZWPWCWAPZWBOZ
      WNWTQWAWBRXBWTWNXAWTWNQZWBWKWLACSWBWKPZXCWKWBBWJTUAXDWMWTWNXDWLWSACWKWLUB
      UCWKWMUBUDUEUFUGUHWOWSACWJBWFUIUJUKWQWKWJWDLZOZWNWJBWDUIZXEWMWKAWJCULUMUN
      AWJCWGUOZUPUQWIWBWAWIWKDURWFCLZAURZWBPXAWIWKXJDWIWKXIGZAURZWKXJGWIWKWLXIU
      SZOZAURZXIWOUSZAURZGXNXPGZAURXLWIXOXQWIWQWRXOXQWEWHWJUTXFWKXMAURZOWQXOXEX
      SWKAWJCVAUMXGWKXMAWKAVBVCVDWRWPXQXHWOACVEUNVFVGXNXPAVHXRXKAWKXNXPXIWKXMVI
      XIWOVJVKVLVMWKXIAVNVOVPDBVQACVQVRVSVT $.
      $( [28-Nov-03] $) $( [25-Nov-03] $)
  $}

  ${
    $d x y A $.
    $( The power class of an intersection in terms of indexed intersection.
       Exercise 24(a) of [Enderton] p. 33. $)
    iinpw $p |- P~ |^| A = |^| x e. A P~ x $=
      ( vy cint cpw cv ciin wss wcel wral ssint visset elpw biral bitr4 cvv wb
      eliin ax-mp 3bitr4 cleqri ) CBDZEZABAFZEZGZCFZUBHZUGUEIZABJZUGUCIUGUFIZUH
      UGUDHZABJUJAUGBKUIULABUGUDCLZMNOUGUBUMMUGPIUKUJQUMAUGBUEPRSTUA $.
      $( [29-Nov-03] $) $( [29-Nov-03] $)

    $( Inclusion of an indexed intersection in the power class of a union.
       Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpwss $p |- U. x e. A P~ x (_ P~ U. A $=
      ( vy cv cpw ciun cuni wss wrex wcel ssiun eliun visset elpw birex bitr
      uniiun sseq2i 3imtr4 ssriv ) CABADZEZFZBGZEZCDZUAHZABIZUFABUAFZHZUFUCJZUF
      UEJZABUAUFKUKUFUBJZABIUHAUFBUBLUMUGABUFUACMZNOPULUFUDHUJUFUDUNNUDUIUFABQR
      PST $.
      $( [25-Nov-03] $) $( [25-Nov-03] $)
  $}

  ${
    $d x y A $.
    iunpw.1 $e |- A e. V $.
    $( The power class of an intersection in terms of indexed intersection.
       Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpw $p |- ( E. x e. A x = U. A <-> P~ U. A = U. x e. A P~ x ) $=
      ( vy cv cuni wceq wrex cpw ciun wss wcel sseq2 biimprcd r19.22sdv com12
      wi ssiun uniiun syl6ssr a1i impbid visset elpw eliun df-pw cleqabi birex
      bitr 3bitr4g cleqrd ssid eleq2 uniex syl5bbr mpbii sylib wa elssuni
      biimp im2an eqss sylibr exp r19.22i syl impbi ) AEZBFZGZABHZVIIZABVHIZJZG
      ZVKDVLVNVKDEZVIKZVPVHKZABHZVPVLLVPVNLZVKVQVSVQVKVSVQVJVRABVJVRVQVHVIVPMNO
      PVSVQQVKVSVPABVHJVIABVHVPRABSTUAUBVPVIDUCUDVTVPVMLZABHVSAVPBVMUEWAVRABVRD
      VMDVHUFUGUHUIUJUKVOVIVMLZABHZVKVOVIVNLZWCVOVIVIKZWDVIULVOVIVLLWDWEVLVNVIU
      MVIVIBCUNZUDUOUPAVIBVMUEUQWBVJABVHBLZWBVJWGWBURVHVIKZVIVHKZURVJWGWHWBWIVH
      BUSWBWIVIVHWFUDUTVAVHVIVBVCVDVEVFVG $.
      $( [5-Dec-03] $) $( [29-Nov-03] $)
  $}



