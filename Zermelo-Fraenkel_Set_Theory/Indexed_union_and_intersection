$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Indexed union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend class notation to include indexed union. $)
  ciun $a class U. x e. A B $.

  $( Extend class notation to include indexed intersection. $)
  ciin $a class |^| x e. A B $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define indexed union.  Ordinarily, ` A ` would be independent of ` x `
       and ` B ` would depend on ` x ` .  In this case ` x ` is called an index,
       ` A ` is called an index set, and ` B ` is called an indexed set.  In
       most books, ` x e. A ` is written as a subscript or underneath the
       ` U. ` .  Definition of [Stoll] p. 45.  $)
    df-iun $a |- U. x e. A B = { y | E. x e. A y e. B } $.

    $( Define indexed intersection.  Definition of [Stoll] p. 45.  See the
       remarks for its sibling operation of indexed union ~ df-iun . $)
    df-iin $a |- |^| x e. A B = { y | A. x e. A y e. B } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.
    $( Membership in indexed union. $)
    eliun $p |- ( A e. U. x e. B C <-> E. x e. B A e. C ) $=
      ( vy ciun wcel cvv wrex elisset wi cv a1i r19.23aiv cab wceq eleq1
      birexdv elabg df-iun eleq2i syl5bb pm5.21nii ) BACDFZGZBHGZBDGZACIZBUDJUG
      UFACUGUFKALCGBDJMNUFBELZDGZACIZEOZGUHUEUKUHEBHUIBPUJUGACUIBDQRSUDULBAECDT
      UAUBUC $.
      $([3-Sep-03]$)

    $( Membership in indexed intersection. $)
    eliin $p |- ( A e. D -> ( A e. |^| x e. B C <-> A. x e. B A e. C ) ) $=
      ( vy cv wcel wral ciin wceq eleq1 biraldv df-iin vtoclabg ) FGZDHZACIBDHZ
      ACIFBACDJEPBKQRACPBDLMAFCDNO $.
      $([9-Jan-04]$)
  $}

  ${
    $d y z A $.  $d x y z B $.  $d z ph $.
    $( The indexed union of a restricted abstraction class. $)
    iunrab $p |- U. x e. A { y e. B | ph } = { y e. B | E. x e. A ph } $=
      ( vz wsb wrex crab cv wcel wa cab ciun df-rab ax-17 hbs1 hbrex weq
      sbequ12 birexdv cbvrab eliun elrabsf birex r19.42v 3bitr biabri 3eqtr4r
      ) ACFGZBDHZFEIFJZEKZUKLZFMABDHZCEIBDACEIZNZUKFEOUOUKCFBEBJZEKZCPZUSFPUOFP
      UJCBDURDKCPACFQRCFSAUJBDACFTUAUBUNFUQULUQKULUPKZBDHUMUJLZBDHUNBULDUPUCVAV
      BBDACBULEUTUDUEUMUJBDUFUGUHUI $.
      $([24-Jan-04]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Equality theorem for indexed union. $)
    iuneq1 $p |- ( A = B -> U. x e. A C = U. x e. B C ) $=
      ( vy wceq cv wcel wrex cab ciun rexeq biabdv df-iun 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $([27-Jun-98]$)

    $( Equality theorem for restricted existential quantifier. $)
    iineq1 $p |- ( A = B -> |^| x e. A C = |^| x e. B C ) $=
      ( vy wceq cv wcel wral cab ciin raleq biabdv df-iin 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $([27-Jun-98]$)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Subclass theorem for indexed union. $)
    ss2iun $p |- ( A. x e. A B (_ C -> U. x e. A B (_ U. x e. A C ) $=
      ( vy wss wral ciun cv wcel wrex hbra1 wi ra4 ssel syl6 r19.22d eliun
      3imtr4g ssrdv ) CDFZABGZEABCHZABDHZUBEIZCJZABKUEDJZABKUEUCJUEUDJUBUFUGABU
      AABLUBAIBJUAUFUGMUAABNCDUEOPQAUEBCRAUEBDRST $.
      $([30-Nov-03]$)

    $( Equality theorem for indexed union. $)
    iuneq2 $p |- ( A. x e. A B = C -> U. x e. A B = U. x e. A C ) $=
      ( wss wral wa ciun wceq ss2iun im2an eqss biral r19.26 bitr 3imtr4 ) CDEZ
      ABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQSGZABFU
      AUFUHABCDLMQSABNOUBUCLP $.
      $([3-Nov-03]$)

    $( Equality theorem for indexed intersection. $)
    iineq2 $p |- ( A. x e. A B = C -> |^| x e. A B = |^| x e. A C ) $=
      ( vy wceq wral cv wcel cab ciin hbra1 wb ra4 eleq2 syl6 imp biralda
      biabdv df-iin 3eqtr4g ) CDFZABGZEHZCIZABGZEJUDDIZABGZEJABCKABDKUCUFUHEUCU
      EUGABUBABLUCAHBIZUEUGMZUCUIUBUJUBABNCDUDOPQRSAEBCTAEBDTUA $.
      $([4-Nov-03]$)
  $}

  ${
    iuneq2i.1 $e |- ( x e. A -> B = C ) $.
    $( Equality inference for indexed union. $)
    iuneq2i $p |- U. x e. A B = U. x e. A C $=
      ( wceq wral ciun rgen iuneq2 ax-mp ) CDFZABGABCHABDHFLABEIABCDJK $.
      $([3-Nov-03]$)

    $( Equality inference for indexed intersection. $)
    iineq2i $p |- |^| x e. A B = |^| x e. A C $=
      ( wceq wral ciin rgen iineq2 ax-mp ) CDFZABGABCHABDHFLABEIABCDJK $.
      $([4-Nov-03]$)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed union. $)
    hbiu1 $p |- ( y e. U. x e. A B -> A. x y e. U. x e. A B ) $=
      ( vz cv wcel wrex cab wal ciun hbre1 hbab df-iun eleq2i bial 3imtr4 ) BFZ
      EFDGZACHZEIZGZUBAJRACDKZGZUDAJTAEBSACLMUCUARAECDNOZUDUBAUEPQ $.
      $([14-Oct-03]$)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed intersection. $)
    hbii1 $p |- ( y e. |^| x e. A B -> A. x y e. |^| x e. A B ) $=
      ( vz cv wcel wral cab wal ciin hbra1 hbab df-iin eleq2i bial 3imtr4 ) BFZ
      EFDGZACHZEIZGZUBAJRACDKZGZUDAJTAEBSACLMUCUARAECDNOZUDUBAUEPQ $.
      $([16-Oct-03]$)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $( ` z w ` are dummy. $)
    dfiun2.1 $e |- B e. V $.
    $( Alternate definition of indexed union when ` B ` is a set. $)
    dfiun2 $p |- U. x e. A B = U. { y | E. x e. A y = B } $=
      ( vw vz cv wcel wrex cab wel wceq wa wex ciun cuni df-rex clel3 exancom
      bitr bilan 19.42v bitr4 biex 19.11 an12 visset weq cleq1 birexdv elab
      3bitr4 3bitr biabi df-iun df-uni 3eqtr4 ) FHZDIZACJZFKFGLZGHZBHZDMZACJZBK
      ZIZNZGOZFKACDPVGQVAVJFVAAHCIZUTNZAOZVKVBVCDMZNZNZAOZGOZVJUTACRVMVPGOZAOVR
      VLVSAVLVKVOGOZNVSUTVTVKUTVNVBNGOVTGUSDESVNVBGTUAUBVKVOGUCUDUEVPAGUFUAVQVI
      GVBVKVNNZNZAOVBWAAOZNVQVIVBWAAUCVPWBAVKVBVNUGUEVHWCVBVHVNACJZWCVFWDBVCGUH
      BGUIVEVNACVDVCDUJUKULVNACRUAUBUMUEUNUOAFCDUPFGVGUQUR $.
      $([27-Jun-98]$)

    $( Alternate definition of indexed intersection when ` B ` is a set. $)
    dfiin2 $p |- |^| x e. A B = |^| { y | E. x e. A y = B } $=
      ( vw vz cv wcel wral cab wceq wrex wel wi wal ciin cint df-ral clel4
      bilim 19.21v bitr4 bial 19.5 bitr wa wex impexp 19.23v bitr3 visset weq
      cleq1 birexdv elab df-rex birim 3bitr biabi df-iin df-int 3eqtr4 ) FHZDIZ
      ACJZFKGHZBHZDLZACMZBKZIZFGNZOZGPZFKACDQVKRVFVOFVFAHCIZVEOZAPZVPVGDLZVMOZO
      ZAPZGPZVOVEACSVRWAGPZAPWCVQWDAVQVPVTGPZOWDVEWEVPGVDDETUAVPVTGUBUCUDWAAGUE
      UFWBVNGWBVPVSUGZAUHZVMOZVNWBWFVMOZAPWHWIWAAVPVSVMUIUDWFVMAUJUKVLWGVMVLVSA
      CMZWGVJWJBVGGULBGUMVIVSACVHVGDUNUOUPVSACUQUFURUCUDUSUTAFCDVAFGVKVBVC $.
      $([28-Jun-98]$)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.
    cbviunv.1 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis. $)
    cbviunv   $p |- U. x e. A B = U. y e. A C $=
      ( vz cv wcel wrex cab ciun weq eleq2d cbvrexv biabi df-iun 3eqtr4 ) GHZDI
      ZACJZGKSEIZBCJZGKACDLBCELUAUCGTUBABCABMDESFNOPAGCDQBGCEQR $.
      $([15-Sep-03]$)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed union. $)
    iunss $p |- ( U. x e. A B (_ C <-> A. x e. A B (_ C ) $=
      ( vy wss wral cv wcel wa wi wal ciun dfss2 biral df-ral impexp bial
      19.21v bitr2 3bitr wex 19.23v wrex eliun df-rex bitr birim bitr4 19.5
      3bitr4 ) CDFZABGZAHBIZEHZCIZJZUODIZKZELZALZABCMZDFZUMUPURKZELZABGUNVEKZAL
      VAULVEABECDNOVEABPVFUTAUTUNVDKZELVFUSVGEUNUPURQRUNVDESTRUAUSALZELUOVBIZUR
      KZELVAVCVHVJEVHUQAUBZURKVJUQURAUCVIVKURVIUPABUDVKAUOBCUEUPABUFUGUHUIRUSAE
      UJEVBDNUKT $.
      $([13-Sep-03]$)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed union. $)
    ssiun $p |- ( E. x e. A C (_ B ->  C (_ U. x e. A B ) $=
      ( vy wss wrex cv wcel wa wex ciun df-rex wi wal pm3.35 imlan exp32 com23
      imp ssel sylan5 19.22i 19.21aiv eliun bitr2 bilim bial 19.37v dfss2
      3bitr4 sylib sylbi ) DCFZABGAHBIZUNJZAKZDABCLZFZUNABMUQEHZDIZUOUTCIZJZNZA
      KZEOZUSUQVEEUPVDAUOVAVBNZVDUNUOVGVDUOVAVGVCUOVAVGVCVAVGJVBUOVAVBPQRSTDCUT
      UAUBUCUDVAVCAKZNZEOVAUTURIZNZEOVFUSVIVKEVHVJVAVJVBABGVHAUTBCUEVBABMUFUGUH
      VEVIEVAVCAUIUHEDURUJUKULUM $.
      $([3-Sep-03]$)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Identity law for subset of an indexed union. $)
    ssiun2 $p |- ( x e. A ->  B (_ U. x e. A B ) $=
      ( vy cv wcel ciun wa wrex ra4e eliun sylibr exp ssrdv ) AEBFZDCABCGZODEZC
      FZQPFZORHRABISRABJAQBCKLMN $.
      $([14-Oct-03]$)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    ssiun2s.1 $e |- ( x = C -> B = D ) $.
    $( Subset relationship for an indexed union. $)
    ssiun2s $p |- ( C e. A ->  D (_ U. x e. A B ) $=
      ( vy wcel ciun wss cv wi ax-17 hbiu1 hbss hbim wceq eleq1 sseq1d bi2imd
      ssiun2 vtoclgf pm2.43i ) DBHZEABCIZJZAKZBHZCUEJZLUDUFLAGDBGKZDHAMUDUFAUDA
      MAGEUEUJEHAMAGBCNOPUGDQZUHUDUIUFUGDBRUKCEUEFSTABCUAUBUC $.
      $([2-Nov-03]$)
  $}

  ${
    $d x y C $.  $d x y A $.  $d y B $.
    $( Subset theorem for an indexed intersection. $)
    ssiin $p |- ( C (_ |^| x e. A B <-> A. x e. A C (_ B ) $=
      ( vy cv wcel ciin wi wal wss wral cvv wb visset eliin ax-mp bilim
      r19.21v bitr4 df-ral bitr bial dfss2 biral 19.21v 19.5 bitr3 3bitr
      3bitr4 ) EFZDGZUKABCHZGZIZEJAFBGZULUKCGZIZIZAJZEJZDUMKDCKZABLZUOUTEUOURAB
      LZUTUOULUQABLZIVDUNVEULUKMGUNVENEOAUKBCMPQRULUQABSTURABUAUBUCEDUMUDVCUREJ
      ZABLUPVFIZAJZVAVBVFABEDCUDUEVFABUAVHUSEJZAJVAVIVGAUPUREUFUCUSAEUGUHUIUJ
      $.
      $([20-Oct-03]$)
  $}

  ${
    $d x y C $.  $d x y A $.  $d y B $.
    $( Subset implication for an indexed intersection. $)
    iinss $p |- ( E. x e. A B (_ C ->  |^| x e. A B (_ C ) $=
      ( vy cv wcel wi wal wrex ciin wss wa wex 19.12 df-rex ax-17 19.28 biex
      bitr4 bial 3imtr4 wral r19.36iv cvv wb visset eliin ax-mp syl5ib 19.20i
      syl dfss2 birex ) EFZCGZUODGZHZEIZABJZUOABCKZGZUQHZEIZCDLZABJVADLUTURABJZ
      EIZVDAFBGZURMZEIZANZVIANZEIUTVGVIAEOUTVHUSMZANVKUSABPVJVMAVHUREVHEQRSTVFV
      LEURABPUAUBVFVCEVFUPABUCZUQVBUPUQABUDUOUEGVBVNUFEUGAUOBCUEUHUIUJUKULVEUSA
      BECDUMUNEVADUMUB $.
      $([22-Oct-03]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( Class union in terms of indexed union.  Definition of [Stoll] p. 43. $)
    uniiun $p |- U. A = U. x e. A x $=
      ( vy cuni wel wrex cab cv ciun dfuni2 df-iun eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $([28-Jun-98]$)

    $( Class intersection in terms of indexed intersection.  Definition of
       [Stoll] p. 44. $)
    intiin $p |- |^| A = |^| x e. A x $=
      ( vy cint wel wral cab cv ciin dfint2 df-iin eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $([28-Jun-98]$)
  $}

  ${
    $d x y A $.  $d y B $.
    $( There is a non-empty class in an indexed collection ` B ( x ) ` iff the
       indexed  union of them is non-empty. $)
    iun0 $p |- ( E. x e. A -. B = (/) <-> -. U. x e. A B = (/) ) $=
      ( vy c0 wceq wn wrex cv wcel wex wa ciun nnull birex df-rex 19.11
      exdistr eliun bitr2 biex 3bitr3 bitr4 3bitr ) CEFGZABHDIZCJZDKZABHAIBJZUH
      LAKZABCMZEFGZUEUHABDCNOUHABPUJUFUKJZDKZULUIUGLZDKAKUOAKZDKUJUNUOADQUIUGAD
      RUPUMDUMUGABHUPAUFBCSUGABPTUAUBDUKNUCUD $.
      $([19-Oct-03]$)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A relationship involving union and indexed intersection.  Exercise 23 of
       [Enderton] p. 33. $)
    iinun $p |- ( A u. |^| B ) = |^| x e. B ( A u. x ) $=
      ( vy cint cun cv ciin wcel wo wral wel r19.32v elun biral visset elint2
      bilor 3bitr4r cvv wb eliin ax-mp 3bitr4 cleqri ) DBCEZFZACBAGZFZHZDGZBIZU
      KUFIZJZUKUIIZACKZUKUGIUKUJIZULDALZJZACKULURACKZJUPUNULURACMUOUSACUKBUHNOU
      MUTULAUKCDPZQRSUKBUFNUKTIUQUPUAVAAUKCUITUBUCUDUE $.
      $([27-Nov-03]$)

    $( A relationship involving union and indexed union.  Exercise 25 of
      [Enderton] p. 33. $)
    iunun $p |- ( ( B = (/) -> A = (/) ) <->
                ( A u. U. B ) = U. x e. B ( A u. x ) ) $=
      ( vy c0 wceq wi cuni cun cv ciun wcel wel wrex wo wn wb imor r19.45zv
      noel2 con2 biorf birexdv bitr3d syl jaoi bicomd sylbi elun birex syl6bbr
      eluni2 bilor bitr eliun 3bitr4g cleqrd wex wa eleq2 eluni ax-17 19.45
      3bitr4 df-rex 3bitr3g biimpd 19.39 orc pm3.26 syl34 19.22i 3syl 19.37v
      sylib 19.23adv nnull 3imtr4g a3d impbi ) CEFZBEFZGZBCHZIZACBAJZIZKZFZWCDW
      EWHWCDJZBLZDAMZACNZOZWJWGLZACNZWJWELZWJWHLZWCWNWKWLOZACNZWPWCWAPZWBOZWNWT
      QWAWBRXBWTWNXAWTWNQZWBWKWLACSWBWKPZXCWKWBBWJTUAXDWMWTWNXDWLWSACWKWLUBUCWK
      WMUBUDUEUFUGUHWOWSACWJBWFUIUJUKWQWKWJWDLZOZWNWJBWDUIZXEWMWKAWJCULUMUNAWJC
      WGUOZUPUQWIWBWAWIWKDURWFCLZAURZWBPXAWIWKXJDWIWKXIGZAURZWKXJGWIWKWLXIUSZOZ
      AURZXIWOUSZAURZGXNXPGZAURXLWIXOXQWIWQWRXOXQWEWHWJUTXFWKXMAURZOWQXOXEXSWKA
      WJCVAUMXGWKXMAWKAVBVCVDWRWPXQXHWOACVEUNVFVGXNXPAVHXRXKAWKXNXPXIWKXMVIXIWO
      VJVKVLVMWKXIAVNVOVPDBVQACVQVRVSVT $.
      $([28-Nov-03]$)
  $}

  ${
    $d x y A $.
    $( The power class of an intersection in terms of indexed intersection.
       Exercise 24(a) of [Enderton] p. 33. $)
    iinpw $p |- P~ |^| A = |^| x e. A P~ x $=
      ( vy cint cpw cv ciin wss wcel wral ssint visset elpw biral bitr4 cvv wb
      eliin ax-mp 3bitr4 cleqri ) CBDZEZABAFZEZGZCFZUBHZUGUEIZABJZUGUCIUGUFIZUH
      UGUDHZABJUJAUGBKUIULABUGUDCLZMNOUGUBUMMUGPIUKUJQUMAUGBUEPRSTUA $.
      $([29-Nov-03]$)

    $( Inclusion of an indexed intersection in the power class of a union.
       Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpwss $p |- U. x e. A P~ x (_ P~ U. A $=
      ( vy cv cpw ciun cuni wss wrex wcel ssiun eliun visset elpw birex bitr
      uniiun sseq2i 3imtr4 ssriv ) CABADZEZFZBGZEZCDZUAHZABIZUFABUAFZHZUFUCJZUF
      UEJZABUAUFKUKUFUBJZABIUHAUFBUBLUMUGABUFUACMZNOPULUFUDHUJUFUDUNNUDUIUFABQR
      PST $.
      $([25-Nov-03]$)
  $}

  ${
    $d x y A $.
    iunpw.1 $e |- A e. V $.
    $( The power class of an intersection in terms of indexed intersection.
       Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpw $p |- ( E. x e. A x = U. A <-> P~ U. A = U. x e. A P~ x ) $=
      ( vy cv cuni wceq wrex cpw ciun wss wcel sseq2 biimprcd r19.22sdv comm
      wi ssiun uniiun sseq2i sylibr a1i impbid visset elpw eliun df-pw cleqabi
      birex bitr 3bitr4g cleqrd ssid eleq2 zfun2 syl5bbr mpbii sylib wa
      elssuni biimp im2an eqss exp r19.22i syl impbi ) AEZBFZGZABHZVIIZABVHIZJZ
      GZVKDVLVNVKDEZVIKZVPVHKZABHZVPVLLVPVNLZVKVQVSVQVKVSVQVJVRABVJVRVQVHVIVPMN
      OPVSVQQVKVSVPABVHJZKVQABVHVPRVIWAVPABSTUAUBUCVPVIDUDUEVTVPVMLZABHVSAVPBVM
      UFWBVRABVRDVMDVHUGUHUIUJUKULVOVIVMLZABHZVKVOVIVNLZWDVOVIVIKZWEVIUMVOVIVLL
      WEWFVLVNVIUNVIVIBCUOZUEUPUQAVIBVMUFURWCVJABVHBLZWCVJWHWCUSVHVIKZVIVHKZUSV
      JWHWIWCWJVHBUTWCWJVIVHWGUEVAVBVHVIVCUAVDVEVFVG $.
      $([5-Dec-03]$)
  $}



