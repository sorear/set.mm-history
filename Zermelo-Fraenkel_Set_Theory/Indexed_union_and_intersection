$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Indexed union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend class notation to include indexed union. $)
  ciun $a class U. x e. A B $.

  $( Extend class notation to include indexed intersection. $)
  ciin $a class |^| x e. A B $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define indexed union.  Ordinarily, ` A ` would be independent of
       ` x ` and ` B ` would dependent on ` x `.  In this case ` x ` is
       called an index, ` A ` is called an index set, and ` B ` is called an
       indexed set.  In most books, ` x e. A ` is usually written as a
       subscript or underneath the ` U. `.  Stoll, p. 45. $)
    df-iun $a |- U. x e. A B = { y | E. x e. A y e. B } $.

    $( Define indexed intersection.  Stoll, p. 45. $)
    df-iin $a |- |^| x e. A B = { y | A. x e. A y e. B } $.
  $}


  ${
    $d x y A $.  $d x y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Equality theorem for indexed union. $)
    iuneq1 $p |- ( A = B -> U. x e. A C = U. x e. B C ) $=
      ( vy wceq cv wcel wrex cab ciun rexeq biabdv df-iun 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $([27-Jun-98]$)

    $( Equality theorem for restricted existential quantifier. $)
    iineq1 $p |- ( A = B -> |^| x e. A C = |^| x e. B C ) $=
      ( vy wceq cv wcel wral cab ciin raleq biabdv df-iin 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $([27-Jun-98]$)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.  $( ` y ` is dummy. $)
    $( Equality theorem for indexed union. $)
    iuneq2 $p |- ( A. x A = B -> U. x e. C A = U. x e. C B ) $=
      ( vy wceq wal cv wcel wrex cab ciun hba1 wb eleq2 a4s birexd biabdv
      df-iun 3eqtr4g ) BCFZAGZEHZBIZADJZEKUCCIZADJZEKADBLADCLUBUEUGEUBUDUFADUAA
      MUAUDUFNABCUCOPQRAEDBSAEDCST $.
      $([27-Jun-98]$)

    $( Equality theorem for restricted existential quantifier. $)
    iineq2 $p |- ( A. x A = B -> |^| x e. C A = |^| x e. C B ) $=
      ( vy wceq wal cv wcel wral cab ciin hba1 wb eleq2 a4s birald biabdv
      df-iin 3eqtr4g ) BCFZAGZEHZBIZADJZEKUCCIZADJZEKADBLADCLUBUEUGEUBUDUFADUAA
      MUAUDUFNABCUCOPQRAEDBSAEDCST $.
      $([27-Jun-98]$)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $( ` z w ` are dummy. $)
    dfiun2.1 $e |- B e. V $.
    $( Alternate definition of indexed union when ` B ` is a set. $)
    dfiun2 $p |- U. x e. A B = U. { y | E. x e. A y = B } $=
      ( vw vz cv wcel wrex cab wel wceq wa wex ciun cuni df-rex clel3 exancom
      bitr bilan 19.42v bitr4 biex 19.11 an12 visset weq cleq1 birexdv abcl
      3bitr4 3bitr biab df-iun df-uni 3eqtr4 ) FHZDIZACJZFKFGLZGHZBHZDMZACJZBKZ
      IZNZGOZFKACDPVGQVAVJFVAAHCIZUTNZAOZVKVBVCDMZNZNZAOZGOZVJUTACRVMVPGOZAOVRV
      LVSAVLVKVOGOZNVSUTVTVKUTVNVBNGOVTGUSDESVNVBGTUAUBVKVOGUCUDUEVPAGUFUAVQVIG
      VBVKVNNZNZAOVBWAAOZNVQVIVBWAAUCVPWBAVKVBVNUGUEVHWCVBVHVNACJZWCVFWDBVCGUHB
      GUIVEVNACVDVCDUJUKULVNACRUAUBUMUEUNUOAFCDUPFGVGUQUR $.
      $([27-Jun-98]$)

    $( Alternate definition of indexed intersection when ` B ` is a set. $)
    dfiin2 $p |- |^| x e. A B = |^| { y | E. x e. A y = B } $=
      ( vw vz cv wcel wral cab wceq wrex wel wi wal ciin cint df-ral clel4
      bilim 19.21v bitr4 bial 19.5 bitr wa wex impexp 19.23v bitr3 visset weq
      cleq1 birexdv abcl df-rex birim 3bitr biab df-iin df-int 3eqtr4 ) FHZDIZA
      CJZFKGHZBHZDLZACMZBKZIZFGNZOZGPZFKACDQVKRVFVOFVFAHCIZVEOZAPZVPVGDLZVMOZOZ
      APZGPZVOVEACSVRWAGPZAPWCVQWDAVQVPVTGPZOWDVEWEVPGVDDETUAVPVTGUBUCUDWAAGUEU
      FWBVNGWBVPVSUGZAUHZVMOZVNWBWFVMOZAPWHWIWAAVPVSVMUIUDWFVMAUJUKVLWGVMVLVSAC
      MZWGVJWJBVGGULBGUMVIVSACVHVGDUNUOUPVSACUQUFURUCUDUSUTAFCDVAFGVKVBVC $.
      $([28-Jun-98]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( Class union in terms of indexed union.  Stoll, p. 43. $)
    uniiun $p |- U. A = U. x e. A x $=
      ( vy cuni wel wrex cab cv ciun dfuni2 df-iun eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $([28-Jun-98]$)

    $( Class intersection in terms of indexed intersection.  Stoll, p. 44. $)
    intiin $p |- |^| A = |^| x e. A x $=
      ( vy cint wel wral cab cv ciin dfint2 df-iin eqtr4 ) BDCAEABFCGABAHZICABJ
      ACBMKL $.
      $([28-Jun-98]$)
  $}



