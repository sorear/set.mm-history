$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cardinal numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c card $. $( Cardinal size function $)
  $c aleph $. $( Aleph function $)

  $( Extend class definition to include the cardinal size function. $)
  ccrd $a class card $.

  $( Extend class definition to include the aleph function. $)
  cale $a class aleph $.

  ${
    $d x y z $.
    $( Define the cardinal number function.  The cardinal number of a set is
       the least ordinal number equinumerous to it.  Definition of [Enderton]
       p. 197.  See ~ cardval for its value, ~ cardval2 for a simpler version
       of its value.  The principle theorem relating cardinality to
       equinumerosity is ~ carden . $)
    df-card $a |- card = { <. x , y >. | y = |^| { z e. On | z ~~ x } } $.
  $}

  ${
    $d x y z $.
    $( Define the aleph function.  Our definition expresses Definition 12 of
       [Suppes] p. 229 in a closed form, from which we derive the recursive
       definition as theorems ~ aleph0 , ~ alephsuc , and ~ alephlim .
       The aleph function provides a one-to-one, onto mapping from the ordinal
       numbers to the infinite cardinal numbers.  Roughly, any aleph is the
       smallest infinite cardinal number whose size is strictly greater than
       any aleph before it.  $)
    df-aleph $a |- aleph = rec ( { <. x , y >. | y = |^| { z e. On |
                   x ~< z } } , om ) $.
  $}

  ${
    $d x y z A $.
    $( The value of the cardinal number function.  Definition 10.4 of
       [TakeutiZaring] p. 85.  See ~ cardval2 for a simpler version of its
       value. $)
    cardval $p |- ( card ` A ) = |^| { x e. On | x ~~ A } $=
      ( vz vy cvv wcel ccrd cfv cv cen wbr con0 crab cint wceq copab wrex
      numth2 intexrab mpbi breq2 birabsdv inteqd fvopabg mpan2 df-card fveq1i
      syl5eq wn c0 fvprc cab wa visset enrefg ax-mp brprc mpbiri biantrud
      biabdv df-rab syl6reqr abid2 syl6eq inton eqtr4d pm2.61i ) BEFZBGHZAIZBJK
      ZALMZNZOVHBCIVJDIZJKZALMZNZODCPZHZVMVIVHVMEFZVSVMOVKALQVTABRVKALSTDCBVQVM
      EEVNBOZVPVLWAVOVKALVNBVJJUAUBUCUDUEBGVRDCAUFUGUHVHUIZVIUJVMBGUKWBVMLNUJWB
      VLLWBVLVJLFZAULZLWBWDWCVKUMZAULVLWBWCWEAWBVKWCWBVKVJVJJKZVJEFWFAUNVJEUOUP
      VJBJUQURUSUTVKALVAVBALVCVDUCVEVDVFVG $.
      $([21-Oct-03]$)

    $( The value of the cardinal number function with an ordinal number as its
       argument.  In this special case the Axiom of Choice is not needed.  $)
    cardonval $p |- ( A e. On -> ( card ` A ) = |^| { x e. On | x ~~ A } ) $=
      ( vz vy con0 wcel cv cen wbr crab cint wceq copab cfv ccrd c0 wn wrex
      enrefg breq1 rcla4ev mpian rnoel3 sylibr wss ssrab oninton mpan syl
      breq2 birabsdv inteqd fvopabg df-card fveq1i syl5eq ) BEFZBCGAGZDGZHIZAEJ
      ZKZLDCMZNZURBHIZAEJZKZBONUQVGEFZVDVGLUQVFPLQZVHUQVEAERZVIUQBBHIZVJBESVEVK
      ABEURBBHTUAUBVEAEUCUDVFEUEVIVHVEAEUFVFUGUHUIDCBVBVGEEUSBLZVAVFVLUTVEAEUSB
      URHUJUKULUMUBBOVCDCAUNUOUP $.
      $([24-Nov-03]$)
  $}

  ${
    $d x A $.
    $( The cardinal number of a set is an ordinal number.  Proposition 10.6(1)
       of [TakeutiZaring] p. 85. $)
    cardon $p |- ( card ` A ) e. On $=
      ( vx ccrd cfv cv cen wbr con0 crab cint cardval wss c0 wceq wn wcel
      ssrab cvv fvex eqeltrr intex mpbir oninton mp2an eqeltr ) ACDZBEAFGZBHIZJ
      ZHBAKZUHHLUHMNOZUIHPUGBHQUKUIRPUFUIRUJACSTUHUAUBUHUCUDUE $.
      $([25-Nov-03]$)

    $( Any set is equinumerous to its cardinal number.  Proposition 10.5 of
       [TakeutiZaring] p. 85. $)
    cardid $p |- ( card ` A ) ~~ A $=
      ( vx ccrd cfv cv cen wbr con0 crab wcel cint cardval wss c0 wceq wn
      ssrab cvv cardon eqeltrr elisseti intex mpbir onint mp2an eqeltr breq1
      elrab pm3.27bd ax-mp ) ACDZBEZAFGZBHIZJZUKAFGZUKUNKZUNBALZUNHMUNNOPZUQUNJ
      UMBHQUSUQRJUQHUKUQHURASTUAUNUBUCUNUDUEUFUOUKHJUPUMUPBUKHULUKAFUGUHUIUJ $.
      $([25-Nov-03]$)

     $( The cardinal of an ordinal number is less than or equal to the
        ordinal number.  Proposition 10.6(3) of [TakeutiZaring] p. 85. $)
     cardonle $p |- ( A e. On -> ( card ` A ) (_ A ) $=
       ( vx con0 wcel ccrd cfv wss cv cen wbr crab cint enrefg wa breq1 elrab
       intss sylbir mpian cardonval sseq1d mpbird ) ACDZAEFZAGBHZAIJZBCKZLZAGZU
       CAAIJZUIACMUCUJNAUGDUIUFUJBACUEAAIOPAUGQRSUCUDUHABATUAUB $.
       $([25-Nov-03]$)
  $}

  $( The cardinality of the empty set is the empty set. $)
  card0 $p |- ( card ` (/) ) = (/) $=
    ( c0 ccrd cfv wss wceq con0 wcel 0elon cardonle ax-mp ss0b mpbi ) ABCZADZMA
    EAFGNHAIJMKL $.
    $([25-Nov-03]$)

  $( The cardinality of a natural number is the number.  Corollary 10.23 of
     [TakeutiZaring] p. 90. $)
  cardnn $p |- ( A e. om -> ( card ` A ) = A ) $=
    ( com wcel ccrd cfv wn wceq csdm wbr cen cardid sdomnen mt2 wpss word wb
    nnord cardon onord ordelpss mpan syl nnsdomo wo con0 nnont wss cardonle
    onsseleq mpbid elnn exp comm eleq1a jaod mpd sylan anidms bitr4d mtbiri
    ord ) ABCZADEZACZFVCAGZVBVDVCAHIZVFVCAJIAKVCALMVBVDVCANZVFVBAOZVDVGPZAQVCOV
    HVIVCARZSVCATUAUBVBVFVGPZVCBCZVBVKVBVCAUCVBVDVEUDZVLVBAUECZVMAUFVNVCAUGZVMA
    UHVCUECVNVOVMPVJVCAUIUAUJUBZVBVDVLVEVDVBVLVDVBVLVCAUKULUMABVCUNUOUPUQURUSUT
    VBVDVEVPVAUP $.
    $([?]$)

  $( The set of natural numbers is a cardinal number.  Theorem 18.11 of [Monk1]
     p. 133. $)
  cardom $p |- ( card ` om ) = om $=
    ( com ccrd cfv wcel wn wceq cen wbr cardid csdm nnsdom sdomnen syl mt2 wss
    wo con0 omelon cardonle ax-mp cardon onssel mpbi ori ) ABCZADZEUEAFZUFUEAGH
    ZAIUFUEAJHUHEUEKUEALMNUFUGUEAOZUFUGPAQDUIRASTUEAAUARUBUCUDT $.
    $([9-Dec-03]$)

  ${
    $d x y A $.
    $( An ordinal is a cardinal number iff it equals its own cardinal number.
       Proposition 10.9 of [TakeutiZaring] p. 85. $)
    oncard $p |- ( E. x A = ( card ` x ) <-> A = ( card ` A ) ) $=
      ( vy cv ccrd cfv wceq wex wss wa cen wbr cardid breq1 mpbiri entr mpan
      con0 crab cint wcel wi cardon onintss ax-mp cardval sseq1i sylibr 3syl
      sseq1 mpbird eleq1 cardonle syl jca eqss 19.23aiv cvv fvex fveq2 cleq2d
      cla4egv mpcom impbi ) BADZEFZGZAHZBBEFZGZVGVJAVGBVIIZVIBIZJVJVGVKVLVGVKVF
      VIIZVGBVEKLZVIVEKLZVMVGVNVFVEKLVEMBVFVEKNOVIBKLVNVOBMVIBVEPQVOCDZVEKLZCRS
      TZVIIZVMVIRUAVOVSUBBUCVQVOCVIVPVIVEKNUDUEVFVRVICVEUFUGUHUIBVFVIUJUKVGBRUA
      ZVLVGVTVFRUAVEUCBVFRULOBUMUNUOBVIUPUHUQBURUAZVJVHVJWAVIURUABEUSBVIURULOVG
      VJABURVEBGVFVIBVEBEUTVAVBVCVD $.
      $([?]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( No member of a cardinal number of a set is equinumerous to the set.
       Proposition 10.6(2) of [TakeutiZaring] p. 85. $)
    cardne $p |- ( A e. ( card ` B ) -> -. A ~~ B ) $=
      ( vx con0 wcel ccrd cfv cen wbr wn cardon onel wss cv crab cint breq1
      onintss cardval sseq1i syl6ibr wb ontri1 mpan sylibd pm2.03d mpcom ) ADEZ
      ABFGZEZABHIZJUIABKZLUHUKUJUHUKUIAMZUJJZUHUKCNZBHIZCDOPZAMUMUPUKCAUOABHQRU
      IUQACBSTUAUIDEUHUMUNUBULUIAUCUDUEUFUG $.
      $([14-Dec-03]$)
  $}

  $( Two sets are equinumerous iff their cardinal numbers are equal.  This
     important theorem expresses the essential concept behind "cardinality"
     or "size".  This theorem appears as Proposition 10.10 of [TakeutiZaring]
     p. 85, Theorem 7P of [Enderton] p. 197, and Theorem 9 of [Suppes] p. 242
     (among others).  The Axiom of Choice is required for its proof.

     The theory of cardinality can also be developed without AC by
     introducing "card" as a primitive notion and stating this theorem as an
     axiom, as is done with the axiom for cardinal numbers in [Suppes] p.
     111.  Finally, if we allow the Axiom of Regularity, we can avoid AC by
     defining the cardinal number of a set as the set of all sets
     equinumerous to it and having least possible rank (definition of kard of
     [Enderton] p. 222). $)
  carden $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) = ( card ` B ) <-> A ~~ B ) ) $=
    ( wcel wa ccrd cfv wceq cen wbr wi breq2 cardid entr mpan2 syl6bi ensymg
    mpi syl5 comm adantr wss mpan wn cardne con2 con0 wb cardon ontri1 mp2an
    sylibr syl syl6 a1i jcad eqss syl6ibr adantl impbid ) ACEZBDEZFAGHZBGHZIZAB
    JKZVBVFVGLVCVFVBVGVFAVDJKZVGVBVFVHAVEJKZVGVDVEAJMVIVEBJKZVGBNZAVEBOPQVBVDAJ
    KZVHANZVDACRSTUAUBVCVGVFLVBVCVGVDVEUCZVEVDUCZFVFVCVGVNVOVCVGBAJKZVNABDRVPVE
    AJKZVNVJVPVQVKVEBAOUDVQVEVDEZUEZVNVRVQVEAUFUGVDUHEZVEUHEZVNVSUIAUJZBUJZVDVE
    UKULUMUNUOVGVOLVCVGVDBJKZVOVLVGWDVMVDABOUDWDVDVEEZUEZVOWEWDVDBUFUGWAVTVOWFU
    IWCWBVEVDUKULUMUNUPUQVDVEURUSUTVA $.
    $([14-Dec-03]$)

  $( Two sets have the dominance relationship if their cardinalities have the
     subset relationship. $)
  carddomi $p |- ( A e. C -> ( ( card ` A ) (_ ( card ` B ) -> A ~<_ B ) ) $=
    ( wcel ccrd cfv wss cdom wbr wa cen endomtr cardid ensymg mpi con0 wi
    cardon ssdomg ax-mp syl2an domentr mpan2 syl exp ) ACDZAEFZBEFZGZABHIZUFUIJ
    AUHHIZUJAUGKIZUGUHHIZUKUFUIAUGUHLUFUGAKIULAMUGACNOUGPDUIUMQARUGUHPSTUAUKUHB
    KIUJBMAUHBUBUCUDUE $.
    $([25-Nov-03]$)

  $( Two sets have the dominance relationship iff their cardinalities have the
     subset relationship.  Equation (i) of [Quine] p. 232. $)
  carddom $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) (_ ( card ` B ) <-> A ~<_ B ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr wi carddomi adantr csdm cen wo wn cardon
    onelss syl5 domnsym syl6 pm2.03d con0 wb ontri1 mp2an syl6ibr adantl wceq
    carden eqimss syl6bir jaod brdom2 syl5ib impbid ) ACEZBDEZFZAGHZBGHZIZABJKZ
    USVDVELUTABCMNVAABOKZABPKZQVDVEVAVFVDVGUTVFVDLUSUTVFVCVBEZRZVDUTVHVFUTVHBAJ
    KZVFRUTVCVBIVJVHBADMVBVCASZTUABAUBUCUDVBUEEVCUEEVDVIUFVKBSVBVCUGUHUIUJVAVGV
    BVCUKVDABCDULVBVCUMUNUOABUPUQUR $.
    $([22-Dec-03]$)

  $( Two sets have the strict dominance relationship iff their cardinalities
     have the membership relationship.  Corollary 19.7(2) of [Eisenberg]
     p. 310. $)
  cardsdom $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) e. ( card ` B ) <-> A ~< B ) ) $=
    ( wcel wa ccrd cfv wss wceq wn cdom wbr cen csdm carddom carden binegd
    bi2and con0 wb cardon onelpsst mp2an brsdom 3bitr4g ) ACEBDEFZAGHZBGHZIZUHU
    IJZKZFZABLMZABNMZKZFUHUIEZABOMUGUJUNULUPABCDPUGUKUOABCDQRSUHTEUITEUQUMUAAUB
    BUBUHUIUCUDABUEUF $.
    $([28-Dec-03]$)

  $( Tricotomy law for dominance and strict dominance. This theorem is
     equivalent to the Axiom of Choice. $)
  domtri $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr csdm wn carddom wb cardsdom ancoms binegd
    con0 cardon ontri1 mp2an syl5bb bitr3d ) ACEZBDEZFZAGHZBGHZIZABJKBALKZMZABC
    DNUGUIUHEZMZULUJUGUMUKUFUEUMUKOBADCPQRUHSEUISEUJUNOATBTUHUIUAUBUCUD $.
    $([4-Jan-04]$)

  $( Trichotomy of equinumerosity and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  Theorem 8 of [Suppes] p. 242. $)
  entri $p |- ( ( A e. C /\ B e. D ) -> ( A ~< B \/ A ~~ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen wo w3o wn cdom domtri biimprd brdom2 syl6ib pm2.15d
    orrd df-3or sylibr ) ACEBDEFZABGHZABIHZJZBAGHZJUCUDUFKUBUEUFUBUFUEUBUFLZABM
    HZUEUBUHUGABCDNOABPQRSUCUDUFTUA $.
    $([?]$)

  $( Trichotomy of dominance and strict dominance. $)
  entri2 $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen w3o cdom wo entri brdom2 biror df-3or bitr4 sylibr
    ) ACEBDEFABGHZABIHZBAGHZJZABKHZUALZABCDMUDSTLZUALUBUCUEUAABNOSTUAPQR $.
    $([?]$)

  $( Trichotomy of dominance.  This theorem is equivalent to the Axiom of
     Choice.  Part of Proposition 4.42(d) of [Mendelson] p. 275. $)
  entri3 $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B \/ B ~<_ A ) ) $=
    ( wcel wa cdom wbr csdm wo entri2 sdomdom imlor syl ) ACEBDEFABGHZBAIHZJOBA
    GHZJABCDKPQOBALMN $.
    $([?]$)

  $( Strict dominance implies ordinal membership. $)
  sdomel $p |- ( ( A e. On /\ B e. On ) -> ( A ~< B -> A e. B ) ) $=
    ( con0 wcel csdm wbr wi wa wss cdom wn ssdomg adantr ontri1 domtri 3imtr3d
    a3d ancoms ) BCDZACDZABEFZABDZGSTHZUBUAUCBAIZBAJFZUBKUAKSUDUEGTBACLMBANBACC
    OPQR $.
    $([?]$)

  $( A set strictly dominates iff its cardinal strictly dominates. $)
  sdomsdomcard $p |- ( A ~< B <-> A ~< ( card ` B ) ) $=
    ( csdm wbr cvv wcel ccrd cfv wn sdomirr brprc mtbiri a3i c0 sdom0 fvprc
    breq2d cen wa wi fvex sdomentr ax-mp exp cardid ensymg mpi syl5 comm
    mpan2i impbid pm5.21nii ) ABCDZBEFZABGHZCDZUNUMUNIZUMAACDAJABCKLMUNUPUQUPAN
    CDAOUQUONACBGPQLMUNUMUPUMUNUPUMBUORDZUPUNUMURUPUOEFUMURSUPTBGUAABUOEUBUCUDU
    NUOBRDZURBUEZUOBEUFUGUHUIUNUPUSUMUTAUOBEUBUJUKUL $.
    $([12-Jan-04]$)

  $( The cardinality of the cardinality of a set equals the cardinality of
     the set.  Proposition 10.11 of [TakeutiZaring] p. 85. $)
  cardcard $p |- ( card ` ( card ` A ) ) = ( card ` A ) $=
    ( cvv wcel ccrd cfv wceq cen wbr cardid wb fvex carden mpan mpbiri wn c0
    fvprc fveq2d card0 syl6eq eqtr4d pm2.61i ) ABCZADEZDEZUDFZUCUFUDAGHZAIUDBCU
    CUFUGJADKUDABBLMNUCOZUEPUDUHUEPDEPUHUDPDADQZRSTUIUAUB $.
    $([19-Dec-03]$)

  $( Cantor's theorem in terms of cardinals.  This theorem tells us that no
     matter how large a cardinal number is, there is a still larger cardinal
     number.  Theorem 18.12 of [Monk1] p. 133. $)
  canth3 $p |- ( A e. B -> ( card ` A ) e. ( card ` P~ A ) ) $=
    ( wcel ccrd cfv cpw csdm wbr canth2g cvv wb zfpowclg cardsdom mpian mpbird
    ) ABCZADEAFZDECZAQGHZABIPQJCRSKABLAQBJMNO $.
    $([?]$)

  ${
    $d x A $.
    $( An infinite cardinal is a limit ordinal.  Equivalent to Exercise 4 of
       [TakeutiZaring] p. 91. $)
    cardlim $p |- ( om (_ ( card ` A ) <-> Lim ( card ` A ) ) $=
      ( vx com ccrd cfv wss wlim cv csuc wceq con0 wrex wn wcel wa cen wbr wi
      sseq2 biimpd infensuc exp wb limom limsssuc ax-mp syl5ibr sylan9r breq2
      adantl sylibrd com3r imp visset sucid eleq2 mpbiri cardcard eleq2i
      sylibr cardne syl a1i pm2.65d nrexdv c0 wo peano1 ssel mpi noel2 w3o
      word cardon onord ordzsl mpbi 3orass ori 3syl ord mpd limomss impbi ) CAD
      EZFZWEGZWFWEBHZIZJZBKLZMWGWFWJBKWFWHKNZOZWJWHWEPQZWFWLWJWNRWLWJWFWNWLWJWF
      WNRWLWJOWFWHWIPQZWNWJWFCWIFZWLWOWJWFWPWEWICSTWLCWHFZWOWPWLWQWOWHUAUBCGWQW
      PUCUDCWHUEUFUGUHWJWNWOUCWLWEWIWHPUIUJUKUBULUMWJWNMZRWMWJWHWEDEZNZWRWJWHWE
      NZWTWJXAWHWINWHBUNUOWEWIWHUPUQWSWEWHAURUSUTWHWEVAVBVCVDVEWFWKWGWFVFWENZWE
      VFJZMWKWGVGZWFVFCNXBVHCWEVFVIVJWEVFVKXCXDXCWKWGVLZXCXDVGWEVMXEWEAVNVOBWEV
      PVQXCWKWGVRVQVSVTWAWBWEWCWD $.
      $([?]$)
  $}

  $( A cardinal strictly dominates its members.  Equivalent to Proposition
     10.37 of [TakeutiZaring] p. 93 (use ~ cardsdom to obtain the exact
     proposition from this one). $)
  cardsdomel $p |- ( A e. On -> ( A ~< B <-> A e. ( card ` B ) ) ) $=
    ( con0 wcel ccrd cfv csdm wbr wss cdom wn ssdom2g wb cardon ontri1 mpan
    domtri 3imtr3d a3d wi cen wa onelss ax-mp syl cardcard eleq2i cardne
    sylbir jca brsdom sylibr a1i impbid sdomsdomcard syl5bb ) ACDZABEFZGHZAURDZ
    ABGHUQUSUTUQUTUSUQURAIZURAJHZUTKZUSKZURACLURCDZUQVAVCMBNZURAOPVEUQVBVDMVFUR
    ACCQPRSUTUSTUQUTAURJHZAURUAHKZUBUSUTVGVHUTAURIZVGURAVFUCVEVIVGTVFAURCLUDUEU
    TAUREFZDVHVJURABUFUGAURUHUIUJAURUKULUMUNABUOUP $.
    $([13-Jan-04]$)

  ${
    $d x A $.
    $( Two ways to express the property of being a cardinal number. $)
    iscard $p |- ( ( card ` A ) = A <-> ( A e. On /\ A. x e. A x ~< A ) ) $=
      ( ccrd cfv wceq con0 wcel wa cv csdm wbr wral cardon eleq1 mpbii
      pm4.71ri wss wb cardonle eqss baibr syl onelon cardsdomel biraldva dfss3
      syl6rbbr bitr3d pm5.32i bitr ) BCDZBEZBFGZULHUMAIZBJKZABLZHULUMULUKFGUMBM
      UKBFNOPUMULUPUMBUKQZULUPUMUKBQZUQULRBSULURUQUKBTUAUBUMUPUNUKGZABLUQUMUOUS
      ABUMUNBGHUNFGUOUSRBUNUCUNBUDUBUEABUKUFUGUHUIUJ $.
      $([14-Jan-04]$)
  $}

  ${
    $d x y A $.
    $( Two ways to express the property of being a cardinal number.
       Definition 8 of [Suppes] p. 225. $)
    iscard2 $p |- ( ( card ` A ) = A <->
                  ( A e. On /\ A. x e. On ( A ~~ x -> A (_ x ) ) ) $=
      ( vy ccrd cfv wceq con0 wcel wa cv cen wbr wss wi wral cardon eleq1
      mpbii pm4.71ri cardonle biantrurd eqss syl6rbbr crab cint ensymg cvv
      visset ax-mp a1i impbid biland breq1 elrab syl5bb birimd impexp syl6bb
      biraldv2 ssint cardval sseq2i bitrd pm5.32i bitr ) BDEZBFZBGHZVGIVHBAJZKL
      ZBVIMZNZAGOZIVGVHVGVFGHVHBPVFBGQRSVHVGVMVHVGBVFMZVMVHVNVFBMZVNIVGVHVOVNBT
      UAVFBUBUCVHBCJZBKLZCGUDZUEZMZVMVNVHVKAVROVMVTVHVKVLAVRGVHVIVRHZVKNVIGHZVJ
      IZVKNWBVLNVHWAWCVKVHWBVIBKLZIWCWAVHWDVJWBVHWDVJVIBGUFVJWDNZVHVIUGHWEAUHBV
      IUGUFUIUJUKULVQWDCVIGVPVIBKUMUNUOUPWBVJVKUQURUSABVRUTUOVFVSBCBVAVBUOVCVDV
      E $.
      $([?]$)
  $}

  ${
    $d x A $.
    $( Value of alternate definition of the cardinal number of a set. $)
    cardval2 $p |- ( card ` A ) = { x e. On | x ~< A } $=
      ( cv ccrd cfv wcel cab csdm wbr con0 crab abid2 wa cardon onel pm4.71ri
      cardsdomel pm5.32i bitr4 biabi df-rab eqtr4 eqtr3 ) ACZBDEZFZAGZUEUDBHIZA
      JKZAUELUGUDJFZUHMZAGUIUFUKAUFUJUFMUKUFUJUEUDBNOPUJUHUFUDBQRSTUHAJUAUBUC
      $.
      $([?]$)
  $}

  ${
    $d x y z A $.
    $( The collection of ordinal numbers dominated by a set is an ordinal
       number.  (In general, not all collections of ordinal numbers are
       ordinal.)  Theorem 56 of [Suppes] p. 227. $)
    ondomon $p |- ( A e. B -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz wcel cv cdom wbr con0 crab word wtr wss wel wa wi wal domtr
      imlan anassrs onelon onelsst imp cvv visset ssdomg ax-mp syl jca sylan
      exp31 comm imp3a breq1 elrab 3imtr4g gen2 dftr2 mpbir ssrab ordon
      trssord mp3an cpw csdm wb wral elisset domsdomtr canth2g sylan5 exp a1d
      r19.21aiv ssrab2 sylibr ccrd cfv cardval2 fvex eqeltrr ssex elong 3syl
      mpbiri ) BCFZAGZBHIZAJKZJFZWJLZWJMZWJJNJLWLWMDEOZEGZWJFZPDGZWJFZQZERDRWSD
      EWNWPWRWNWOJFZWOBHIZPWQJFZWQBHIZPZWPWRWNWTXAXDWTWNXAXDQWTWNXAXDXBWQWOHIZP
      XAXDWTWNPZXBXEXAXDXEXAPXCXBWQWOBSTUAXFXBXEWOWQUBXFWQWONZXEWTWNXGWOWQUCUDW
      QUEFXGXEQDUFWQWOUEUGUHUIUJUKULUMUNWIXAAWOJWHWOBHUOUPWIXCAWQJWHWQBHUOUPUQU
      DURDEWJUSUTWIAJVAVBWJJVCVDWGWJWHBVEZVFIZAJKZNZWJUEFWKWLVGWGWIXIQZAJVHZXKW
      GBUEFZXMBCVIXNXLAJXNXLWHJFWIXNXIWIXNXIWIBXHVFIXIXNWHBXHVJBUEVKVLVMUMVNVOU
      IWIXIAJVPVQWJXJXHVRVSXJUEAXHVTXHVRWAWBWCWJUEWDWEWF $.
      $([?]$)
  $}

  ${
    $d x y z A $.
    $( The class of ordinal numbers dominated by a set is a cardinal number.
       Theorem 59 of [Suppes] p. 228. $)
    ondomcard $p |- ( A e. B ->
                 ( card ` { x e. On | x ~<_ A } ) = { x e. On | x ~<_ A } ) $=
      ( vy wcel cvv cv cdom wbr con0 crab ccrd cfv wceq elisset cen wss wi
      wral wa ondomon wn csdm domsdomtr breq1 elrab pm3.27bd word eloni
      ordeirr syl hbrab1 ax-17 hbbr elrabf biimpr exp mtod wb domtri bicon2d
      mpcom mpbird syl2an sdomnen comm pm2.03d visset ensymg ax-mp syl5 adantr
      ontri1 sylan sylibrd r19.21aiv jca iscard2 sylibr ) BCEBFEZAGZBHIZAJKZLMW
      CNZBCOVTWCJEZWCDGZPIZWCWFQZRZDJSZTWDVTWEWJABFUAZVTWIDJVTWFJEZWIVTWLTWGWFW
      CEZUBZWHVTWGWNRWLVTWFWCPIZWNWGVTWMWOWMVTWOUBZWMVTWPWMVTTWFWCUCIZWPWFBHIZB
      WCUCIZWQWMVTWFBWCUDWMWLWRWBWRAWFJWAWFBHUEUFUGVTWSWCBHIZUBZVTWEXAWKWEWTWCW
      CEZWEWCUHXBUBWCUIWCUJUKWEWTXBXBWEWTTWBWTADWCJWBADJULZWLAUMADWCBHXCWFHEAUM
      WFBEAUMUNWAWCBHUEUOUPUQURUKWEVTWSXAUSZWKWEVTXDWEVTTWTWSWCBJFUTVAUQVBVCVDW
      FWCVEUKUQVFVGWFFEWGWORDVHWCWFFVIVJVKVLWEWLWHWNUSVTWCWFVMWKVNVOUQVPVQDWCVR
      VSUK $.
      $([?]$)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The union of a set of cardinals is a cardinal.  Theorem 18.14 of [Monk1]
       p. 133. $)
    carduni $p |- ( A e. B ->
                  ( A. x e. A ( card ` x ) = x -> ( card ` U. A ) = U. A ) ) $=
      ( vy wcel cv ccrd cfv wceq wral cuni wa wss wn con0 onunit weq fveq2 id
      cleq12d rcla4v cardon eleq1 mpbii syl6 ssrdv syl5 imp cardonle syl
      oneirr wi wex eluni comm cdom wbr cvv wb zfun2g visset carddom mpan
      bicomd sseq1 sylan9bb elssuni ssdomg ax-mp syl5bi ssel exp com13 syld
      com4r 19.23aiv sylbi mtoi jca word eloni onord ordtri4 3syl mpbird ) BCEZ
      AFZGHZWGIZABJZBKZGHZWKIZWFWJLZWMWLWKMZWLWKEZNZLZWNWOWQWNWKOEZWOWFWJWSWFBO
      MWSWJBCPWJDBOWJDFZBEZWTGHZWTIZWTOEZWIXCAWTBADQZWHXBWGWTWGWTGRXESTUAZXCXBO
      EXDWTUBXBWTOUCUDUEUFUGUHZWKUIUJWNWPWLWLEZWLWKUBZUKWFWJWPXHULWPWJWFXHWPWLW
      TEZXALZDUMWJWFXHULULZDWLBUNXKXLDXJXAXLXAWJWFXJXHXAWJXCWFXJXHULZULWJXAXCXF
      UOWFXCXAXMWFXCXAXMULWFXCLZXAWTWLMZXMXNWTWKUPUQZXOXAWFXPXBWLMZXCXOWFWKUREZ
      XPXQUSBCUTXRXQXPWTUREZXRXQXPUSDVAZWTWKURURVBVCVDUJXBWTWLVEVFXAWTWKMZXPWTB
      VGXSYAXPULXTWTWKURVHVIUJVJWTWLWLVKUEVLVMVNVOUHVPVQVMUHVRVSWNWSWKVTZWMWRUS
      ZXGWKWAWLVTYBYCWLXIWBWLWKWCVCWDWEVL $.
      $([19-Jan-04]$)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( The indexed union of a set of cardinals is a cardinal. $)
    cardiun $p |- ( A e. C -> ( A. x e. A ( card ` B ) = B ->
                  ( card ` U. x e. A B ) = U. x e. A B ) ) $=
      ( vz vy wcel ccrd cfv wceq wral ciun wa cv wrex cab cuni cvv abrexexg
      visset weq cleq1 birexdv elab wi fveq2 cardcard syl6eq id eqtr4d a1i
      r19.23aiv sylbi rgen carduni mpi syl fvex dfiun2 fveq2i 3eqtr4g adantr
      iuneq2 adantl fveq2d 3eqtr3d exp ) BDGZCHIZCJABKZABCLZHIZVKJVHVJMZABVILZH
      IZVNVLVKVHVOVNJVJVHENZVIJZABOZEPZQZHIZVTVOVNVHVSRGZWAVTJZAEBVIDSWBFNZHIZW
      DJZFVSKWCWFFVSWDVSGWDVIJZABOZWFVRWHEWDFTEFUAVQWGABVPWDVIUBUCUDWGWFABWGWFU
      EANBGWGWEVIWDWGWEVIHIVIWDVIHUFCUGUHWGUIUJUKULUMUNFVSRUOUPUQVNVTHAEBVICHUR
      USZUTWIVAVBVMVNVKHVJVNVKJVHABVICVCVDZVEWJVFVG $.
      $([21-Jan-04]$)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The smallest ordinal that strictly dominates a set is a
       cardinal. $)
    cardmin $p |- ( A e. B -> ( card ` |^| { x e. On | A ~< x } ) =
                      |^| { x e. On | A ~< x } ) $=
      ( vy wcel cv csdm wbr con0 crab cint wral wa ccrd cfv wceq wrex numthcor
      onintrab2 sylib cdom wn wi onelon exp syl breq2 elrab wss ssrab onnmin
      mpan sylbir pm2.03d syl6 pm2.43d cvv wb visset domtri sylibrd ax-17
      hbrab1 hbint hbbr onminsb a1d jcad domsdomtr r19.21aiv jca iscard sylibr
      ) BCEZBAFZGHZAIJZKZIEZDFZVRGHZDVRLZMVRNOVRPVNVSWBVNVPAIQZVSABCRZVPASTZVNW
      ADVRVNVTVREZVTBUAHZBVRGHZMWAVNWFWGWHVNWFBVTGHZUBZWGVNWFWJVNWFVTIEZWFWJUCV
      NVSWFWKUCWEVSWFWKVRVTUDUEUFWKWIWFWKWIWFUBZWKWIMVTVQEZWLVPWIAVTIVOVTBGUGUH
      VQIUIWMWLVPAIUJVQVTUKULUMUEUNUOUPVTUQEVNWGWJURDUSVTBUQCUTULVAVNWHWFVNWCWH
      WDVPWHAADBVRGVTBEAVBVTGEAVBADVQVPADIVCVDVEVOVRBGUGVFUFVGVHVTBVRVIUOVJVKDV
      RVLVM $.
      $([15-Jan-04]$)
  $}

  ${
    $d x y $.
    $( The class of cardinal numbers is not a set (i.e. is a proper class).
       Theorem 19.8 of [Eisenberg] p. 310. $)
    cardprc $p |- -. { x | ( card ` x ) = x } e. V $=
      ( vy cv ccrd cfv wceq cab cvv wcel cuni cpw canth3 wss wn cdom wbr fvex
      cardcard wb ax-17 hbab1 hbuni hbpw hbfv hbeq fveq2 id cleq12d elabgf
      ax-mp mpbir elssuni ssdomg mp2 carddom mpan mpbiri sseq1i sylib con0
      cardon ontri1 mp2an pm2.65i zfun2g mto ) ACZDEZVGFZAGZHIVJJZHIZVLVKDEZVKK
      ZDEZIZVKHLVLVOVMMZVPNZVLVODEZVMMZVQVLVTVOVKOPZVOHIZVOVKMZWAVNDQZVOVJIZWCW
      EVSVOFZVNRZWBWEWFSWDVIWFABVOHABVNDBCDIATZABVKABVJVIABUAUBUCUDZABBVSVOABVO
      DWHWIUDWIUEVGVOFZVHVSVGVOVGVODUFWJUGUHUIUJUKVOVJULUJVOVKHUMUNWBVLVTWASWDV
      OVKHHUOUPUQVSVOVMWGURUSVOUTIVMUTIVQVRSVNVAVKVAVOVMVBVCUSVDVJHVEVF $.
      $([?]$)
  $}

  ${
    $d x y z $.
    $( The aleph function is a function on the class of ordinal numbers. $)
    alephfnon $p |- aleph Fn On $=
      ( vy vx vz cale con0 wfn cv csdm wbr crab cint wceq copab com crdg
      rdgfnon wb df-aleph fneq1 ax-mp mpbir ) DEFZAGBGCGHICEJKLBAMZNOZEFZNUCPDU
      DLUBUEQBACREDUDSTUA $.
      $([22-Oct-03]$)

    $( The first infinite cardinal number, discovered by Georg Cantor in 1873,
       has the same size as the set of natural numbers ` om ` (and under our
       particular definition is also equal to it).  In the literature the
       argument of the aleph function is often written as a subscript, and the
       first aleph is written ` aleph `_0.  Exercise 3 of [TakeutiZaring]
       p. 91.  Also Definition 12(i) of [Suppes] p. 228.  From Kuratowski and
       Mostowski, _Set Theory_, p. 95:  "Aleph...the first letter in the Hebrew
       alphabet...is also the first letter of the Hebrew word...(_einsoph_,
       meaning infinity), which is a cabbalistic appellation of the deity.  The
       notation is due to Cantor, who was deeply interested in mysticism."  $)
    aleph0 $p |- ( aleph ` (/) ) = om $=
      ( vy vx vz c0 cale cfv cv csdm wbr con0 crab cint wceq copab com crdg
      df-aleph fveq1i omex rdgzer eqtr ) DEFDAGBGCGHICJKLMBANZOPZFODEUCBACQROUB
      STUA $.
      $([21-Oct-03]$)
  $}

  ${
    $d x y z w A $.
    $( Value of the aleph function at a limit ordinal.  Definition 12(iii) of
       [Suppes] p. 91. $)
    alephlim $p |- ( ( A e. B /\ Lim A ) -> ( aleph ` A ) =
                   U. x e. A ( aleph ` x ) ) $=
      ( vz vy vw wcel wlim wa cv csdm wbr con0 crab cint wceq copab com crdg
      cfv ciun cale rdglim2a df-aleph fveq1i a1i iuneq2i 3eqtr4g ) BCGBHIBDJEJF
      JKLFMNOPEDQZRSZTABAJZUJTZUABUBTABUKUBTZUAARBCUIUCBUBUJEDFUDZUEABUMULUMULP
      UKBGUKUBUJUNUEUFUGUH $.
      $([22-Oct-03]$)

    $( An aleph is an ordinal number. $)
    alephon $p |- ( aleph ` A ) e. On $=
      ( vx vy vz vw con0 wcel cale cfv cv c0 csuc wceq fveq2 eleq1d weq com
      aleph0 omelon eqeltr csdm wbr crab cint cvv wb wi wa ax-17 wel df-aleph
      breq1 birabsdv inteqd rdgsucopab onintrab syl6rbbr exp ibib sylibr wn
      0elon ax17el rdgsucopabn mpbiri a1i pm2.61d a1d wlim ciun wal visset
      alephlim mpan wral df-ral fvex iunon sylbir syl5bir tfinds cdm wfn
      alephfnon fndm ax-mp eleq2i bineg ndmfv pm2.61i ) AFGZAHIZFGZBJZHIZFGZKHI
      ZFGCJZHIZFGZWRLZHIZFGZWMBCAWNKMWOWQFWNKHNOBCPWOWSFWNWRHNOWNXAMWOXBFWNXAHN
      OWNAMWOWLFWNAHNOWQQFRSTWRFGZXCWTXDWSWNUAUBZBFUCZUDZUEGZXCXDXHXHXCUFZUGXHX
      CUGXDXHXIXDXHUHZXCXGFGXHXJXBXGFDCEQWRDJZWNUAUBZBFUCZUDZXGUEHEJZQGDUIZECUJ
      DUIXOXGGDUIZDCBUKZXKWSMZXMXFXSXLXEBFXKWSWNUAULUMUNZUOOXEBUPUQURXHXCUSUTXH
      VAZXCUGXDYAXCKFGZVBYAXBKFDCEQWRXNXGHXPECDVCXQXRXTVDOVEVFVGVHWNVIZWPCWNWSV
      JZFGZCBUJWTUGCVKZYCWOYDFWNUEGYCWOYDMBVLZCWNUEVMVNOYFWTCWNVOYEWTCWNVPCWNWS
      YGWRHVQVRVSVTWAWKVAZWMYBVBYHWLKFYHAHWBZGZVAWLKMYJWKYIFAHFWCYIFMWDFHWEWFWG
      WHAHWIVSOVEWJ $.
      $([10-Dec-03]$)

    $( Value of the aleph function at a successor ordinal.  Definition 12(ii)
       of [Suppes] p. 91. $)
    alephsuc $p |- ( A e. On -> ( aleph ` suc A ) =
                    |^| { x e. On | ( aleph ` A ) ~< x } ) $=
      ( vy vz vw con0 wcel cale cfv cv csdm wbr crab cint cvv csuc wceq wrex
      fvex numthcor ax-mp intexrab mpbi com ax-17 df-aleph breq1 birabsdv
      inteqd rdgsucopab mpan2 ) BFGBHIZAJZKLZAFMZNZOGZBPHIUPQUNAFRZUQULOGURBHSA
      ULOTUAUNAFUBUCCDEUDBCJZUMKLZAFMZNUPOHEJZUDGCUEVBBGCUEVBUPGCUECDAUFUSULQZV
      AUOVCUTUNAFUSULUMKUGUHUIUJUK $.
      $([11-Nov-03]$)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Every aleph is a cardinal number.  Theorem 65 of [Suppes] p. 229. $)
    alephcard $p |- ( card ` ( aleph ` A ) ) = ( aleph ` A ) $=
      ( vx vy con0 wcel cale cfv ccrd wceq cv c0 csuc fveq2 fveq2d cleq12d weq
      com cardom aleph0 fveq2i 3eqtr4 csdm wbr crab cint cvv fvex cardmin
      ax-mp a1i alephsuc 3eqtr4d a1d wlim wral wel wi wal wa ciun visset
      cardiun adantl alephlim mpan adantr exp df-ral syl5ibr tfinds wn card0
      cdm wfn alephfnon fndm eleq2i bineg ndmfv sylbir mpbiri pm2.61i ) ADEZAFG
      ZHGZWDIZBJZFGZHGZWHIZKFGZHGZWKICJZFGZHGZWNIZWMLZFGZHGZWRIZWFBCAWGKIZWIWLW
      HWKXAWHWKHWGKFMZNXBOBCPZWIWOWHWNXCWHWNHWGWMFMZNXDOWGWQIZWIWSWHWRXEWHWRHWG
      WQFMZNXFOWGAIZWIWEWHWDXGWHWDHWGAFMZNXHOQHGQWLWKRWKQHSTSUAWMDEZWTWPXIWNWGU
      BUCBDUDUEZHGZXJWSWRXKXJIZXIWNUFEXLWMFUGBWNUFUHUIUJXIWRXJHBWMUKZNXMULUMWGU
      NZWPCWGUOZWJCBUPWPUQCURXNXOWJXNXOUSZCWGWNUTZHGZXQWIWHXOXRXQIZXNWGUFEZXOXS
      UQBVAZCWGWNUFVBUIVCXPWHXQHXNWHXQIZXOXTXNYBYACWGUFVDVEVFZNYCULVGWPCWGVHVIV
      JWCVKZWFKHGZKIVLYDWEYEWDKYDWDKHYDAFVMZEZVKWDKIYGWCYFDAFDVNYFDIVODFVPUIVQV
      RAFVSVTZNYHOWAWB $.
      $([22-Jan-04]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( No cardinal can be sandwiched between an aleph and its successor aleph.
       Theorem 67 of [Suppes] p. 229. $)
    alephnbtwn $p |- ( ( card ` B ) = B ->
                   -. ( ( aleph ` A ) e. B /\ B e. ( aleph ` suc A ) ) ) $=
      ( vx con0 wcel ccrd cfv wceq cale csuc wa wn wi csdm wbr wb eleq2
      alephon cardsdomel ax-mp syl5bb adantl cv crab cint alephsuc eleq2d
      biimpd breq2 onnminsb sylan9 pm2.03d cardon eleq1 mpbii sylan5 sylbird
      imnan sylib exp c0 noel2 cdm wfn alephfnon fndm eleq2i bineg ndmfv
      sylbir nsyl2 sucelon sylibr con3 a1d pm2.61i ) ADEZBFGZBHZAIGZBEZBAJZIGZE
      ZKZLZMVQVSWFVQVSKZWAWDLZMWFWGWAVTBNOZWHVSWIWAPVQVSVTVREZWAWIVRBVTQVTDEWIW
      JPARVTBSTUAUBVQBDEZWIWHMVSVQWKKWDWIVQWDBVTCUCZNOZCDUDUEZEZWKWILVQWDWOVQWC
      WNBCAUFUGUHWMWICBWLBVTNUIUJUKULVSVRDEWKBUMVRBDUNUOUPUQWAWDURUSUTVQLWFVSWE
      VQWDVQWAWDWBDEZVQWDWCVAHZWPWCBVBWPLWBIVCZEZLWQWSWPWRDWBIDVDWRDHVEDIVFTVGV
      HWBIVIVJVKAVLVMUBVNVOVP $.
      $([25-Jan-04]$)
  $}

  $( No set has equinumerosity between an aleph and its aleph successor. $)
  alephnbtwn2 $p |- -. ( ( aleph ` A ) ~< B /\ B ~< ( aleph ` suc A ) ) $=
    ( cvv wcel cale cfv csdm wbr csuc wa wn ccrd wceq cardcard alephnbtwn
    ax-mp wb con0 alephon cardsdomel a1i cardsdom mpan2 alephcard eleq2i
    syl5rbbr bi2and mtbiri relsdom elbrexi adantl con3 pm2.61i ) BCDZAEFZBGHZBA
    IZEFZGHZJZKUNUTUOBLFZDZVAURDZJZVALFVAMVDKBNAVAOPUNUPVBUSVCUPVBQZUNUORDVEASU
    OBTPUAUNVAURLFZDZUSVCUNURRDVGUSQUQSBURCRUBUCVFURVAUQUDUEUFUGUHUTUNUSUNUPBUR
    GUIUJUKULUM $.
    $([8-Feb-04]$)

  ${
    $d x y z A $.  $d x y z B $.
    $( Lemma for ~ alephordi . $)
    alephordlem1 $p |- ( A e. On -> ( aleph ` A ) ~< ( aleph ` suc A ) ) $=
      ( vx vy con0 wcel cale cfv csuc csdm wbr cv crab cint wrex alephon
      numthcor ax-mp ax-17 hbrab1 hbint hbbr breq2 onminsb alephsuc breq2d
      mpbiri ) ADEZAFGZAHFGZIJUHUHBKZIJZBDLZMZIJZUKBDNZUNUHDEUOAOBUHDPQUKUNBBCU
      HUMICKZUHEBRUPIEBRBCULUKBCDSTUAUJUMUHIUBUCQUGUIUMUHIBAUDUEUF $.
      $([9-Feb-04]$)

    $( Lemma for ~ alephordi . $)
    alephordlem2 $p |- ( ( B e. V /\ Lim B ) ->
                       ( A e. B -> ( aleph ` A ) ~<_ ( aleph ` B ) ) )  $=
      ( vx cvv wcel wlim wa cale cfv wss cdom wbr cv ciun alephlim sseq2d
      fveq2 ssiun2s syl5bir con0 wi alephon ssdomg ax-mp syl6 ) BDEBFGZABEZAHIZ
      BHIZJZUHUIKLZUFUJUHCBCMZHIZNZJUGUFUIUNUHCBDOPCBUMAUHULAHQRSUHTEUJUKUAAUBU
      HUITUCUDUE $.
      $([9-Feb-04]$)

    $( Ordering property of the aleph function. $)
    alephordi $p |- ( B e. On ->
                    ( A e. B -> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
      ( vx vy cv wcel cale cfv csdm wbr wi c0 csuc wceq eleq2 fveq2 breq2d
      bi2imd weq noel pm2.21i con0 wo sdomtr alephordlem1 sylan5 exp comm
      syl3d com23 breq1d syl5bir a1d com3r jaod visset elsuc2 syl5ib wlim wel
      wal cdom cen wn wa wss ciun cvv alephlim mpan sseq2d ssiun2s alephon
      ssdomg ax-mp syl6 limsuc alephordlem2 sylbid imp domnsym syl onelon
      limelon sylan fvex ensymg ensdomtr syl5 mtod jcad brsdom syl6ibr tfinds
      ) ACEZFZAGHZWOGHZIJZKZALFZWQLGHZIJZKADEZFZWQXDGHZIJZKZAXDMZFZWQXIGHZIJZKA
      BFZWQBGHZIJZKCDBWOLNZWPXAWSXCWOLAOXPWRXBWQIWOLGPQRCDSZWPXEWSXGWOXDAOXQWRX
      FWQIWOXDGPQRWOXINZWPXJWSXLWOXIAOXRWRXKWQIWOXIGPQRWOBNZWPXMWSXOWOBAOXSWRXN
      WQIWOBGPQRXAXCATUAXDUBFZXJXHXLXTXEAXDNZUCXHXLKZXJXTXEYBYAXTXHXEXLXTXGXLXE
      XGXTXLXGXTXLXGXFXKIJZXLXTWQXFXKUDXDUEZUFUGUHUIUJYAXHXTXLYAXTXLKXHYAXLYCXT
      YAWQXFXKIAXDGPUKYDULUMUNUOXDADUPUQURUJWOUSZWTDCUTXHKDVAYEWPWQWRVBJZWQWRVC
      JZVDZVEWSYEWPYFYHYEWPWQWRVFZYFYEYIWQDWOXFVGZVFWPYEWRYJWQWOVHFZYEWRYJNCUPZ
      DWOVHVIVJVKDWOXFAWQXDAGPVLULWQUBFYIYFKAVMWQWRUBVNVOVPYEWPYHYEWPVEZYGWRAMZ
      GHZIJZYMYOWRVBJZYPVDYEWPYQYEWPYNWOFZYQWOAVQYKYEYRYQKYLYNWOVRVJVSVTYOWRWAW
      BYMAUBFZYGYPKWOUBFZWPYSYEWOAWCYKYEYTKYLWOVHWDVOWEYGYSYPYGWQYOIJZYPYSYGWRW
      QVCJZUVAYPKWRVHFYGUVBKWOGWFWQWRVHWGVOUVBUVAYPWRWQYOWHUGWBAUEWIUHWBWJUGWKW
      QWRWLWMUMWN $.
      $([9-Feb-04]$)
  $}

  $( Ordering property of the aleph function. $)
  alephord $p |- ( ( A e. On /\ B e. On ) ->
                 ( A e. B <-> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr wi alephordi adantl cdom cen wn wss wceq
    pm2.16d wb alephon domtri mp2an syl5ib adantr ontri1 sylibrd fveq2 eqeng
    ax-mp syl con3 a1i im2and onelpsst brsdom impbid ) ACDZBCDZEZABDZAFGZBFGZHI
    ZURUTVCJUQABKLUSVAVBMIZVAVBNIZOZEZUTVCUSVGABPZABQZOZEUTUSVDVHVFVJUSVDBADZOZ
    VHUQVDVLJURUQVBVAHIZOZVLVDUQVKVMBAKRVACDZVBCDVDVNSATZBTVAVBCCUAUBUCUDABUEUF
    VFVJJUSVIVEVIVAVBQZVEABFUGVOVQVEJVPVAVBCUHUIUJUKULUMABUNUFVAVBUOUCUP $.
    $([?]$)

  $( Ordering property of the aleph function.  Theorem 8A(a) of [Enderton]
     p. 213 and its converse. $)
  alephord2 $p |- ( ( A e. On /\ B e. On ) ->
                  ( A e. B <-> ( aleph ` A ) e. ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr alephord ccrd wb alephon cardsdomel ax-mp
    alephcard eleq2i bitr syl6bb ) ACDBCDEABDAFGZBFGZHIZTUADZABJUBTUAKGZDZUCTCD
    UBUELAMTUANOUDUATBPQRS $.
    $([?]$)

  $( Ordering property of the aleph function.  Theorem 66 of [Suppes]
     p. 229.$)
  alephord2i $p |- ( B e. On ->
                   ( A e. B -> ( aleph ` A ) e. ( aleph ` B ) ) ) $=
    ( con0 wcel cale cfv wa onelon wi alephord2 biimpd exp imp3a mpcom ) BCDZAB
    DZAEFBEFDZACDZOPGQBAHROPQROPQIROGPQABJKLMNL $.
    $([?]$)

  $( Ordering property of the aleph function. $)
  alephord3 $p |- ( ( A e. On /\ B e. On ) ->
                  ( A (_ B <-> ( aleph ` A ) (_ ( aleph ` B ) ) ) $=
    ( con0 wcel wa wn cale cfv wss wb alephord2 ancoms binegd ontri1 alephon
    mp2an a1i 3bitr4d ) ACDZBCDZEZBADZFBGHZAGHZDZFZABIUDUCIZUAUBUETSUBUEJBAKLMA
    BNUGUFJZUAUDCDUCCDUHAOBOUDUCNPQR $.
    $([?]$)

  $( A set dominated by an aleph is strictly dominated by its successor
     aleph. $)
  alephsucdom $p |- ( B e. On ->
                    ( A ~<_ ( aleph ` B ) <-> A ~< ( aleph ` suc B ) ) ) $=
    ( cvv wcel con0 cale cfv cdom wbr csuc csdm wb wi wa domsdomtr exp
    alephordlem1 syl5 comm adantl wn fvex domtri mpan2 alephnbtwn2 imnan mpbir
    con2 syl5bir adantr impbid reldom elbrexi relsdom pm5.21ni a1d pm2.61i ) AC
    DZBEDZABFGZHIZABJFGZKIZLZMURUSVDURUSNVAVCUSVAVCMURVAUSVCVAUTVBKIZVCUSVAVEVC
    AUTVBOPBQRSTURVCVAMUSURVAUTAKIZUAZVCURUTCDVAVGLBFUBAUTCCUCUDVFVCVFVCUAMVFVC
    NUABAUEVFVCUFUGUHUIUJUKPURUAVDUSVAURVCAUTHULUMAVBKUNUMUOUPUQ $.
    $([?]$)

  ${
    $d x A $.
    $( Value of the successor of an alternate definition of the aleph
       function. $)
    alephsuc2 $p |- ( A e. On ->
                ( aleph ` suc A ) = { x e. On | x ~<_ ( aleph ` A ) } ) $=
      ( con0 wcel cv cale cfv cdom wbr crab csuc csdm alephsucdom birabsdv
      ccrd alephcard cardval2 eqtr3 syl6reqr ) BCDZAEZBFGHIZACJUABKZFGZLIZACJZU
      DTUBUEACUABMNUDOGUDUFUCPAUDQRS $.
      $([?]$)
  $}

  $( An aleph is greater than or equal to the set of natural numbers. $)
  alephgeom $p |- ( A e. On <-> om (_ ( aleph ` A ) ) $=
    ( con0 wcel com cale cfv wss c0 nullss wb 0elon alephord3 mpan mpbii
    aleph0 sseq1i sylib cdm wn peano1 word ordom ord0 ordtri1 mp2an bicon2
    mpbi ndmfv sseq2d mtbiri a3i wfn wceq alephfnon fndm ax-mp eleq2i impbi )
    ABCZDAEFZGZUSHEFZUTGZVAUSHAGZVCAIHBCUSVDVCJKHALMNVBDUTOPQVAAERZCZUSVFVAVFSZ
    VADHGZHDCZVHSTVHVIDUAHUAVHVISJUBUCDHUDUEUFUGVGUTHDAEUHUIUJUKVEBAEBULVEBUMUN
    BEUOUPUQQUR $.
    $([?]$)

  $( An aleph is a limit ordinal. $)
  alephislim $p |- ( A e. On <-> Lim ( aleph ` A ) ) $=
    ( con0 wcel com cale cfv wss wlim alephgeom ccrd cardlim alephcard sseq2i
    wceq wb limeq ax-mp 3bitr3 bitr ) ABCDAEFZGZTHZAIDTJFZGUCHZUAUBTKUCTDALZMUC
    TNUDUBOUEUCTPQRS $.
    $([?]$)

  ${
    $d x y A $.
    $( The argument of the aleph function is less than its value.  Exercise 2
       of [TakeutiZaring] p. 91. $)
    alephle $p |- ( A e. On -> A (_ ( aleph ` A ) ) $=
      ( vx vy cv cale cfv wss weq id fveq2 sseq12d wceq con0 wcel wral wel wa
      wi alephord2i imp onelon alephon ontr2 mpan2 syl exp3a com23 mpd
      r19.20dva wn wb ontri1 oneirr eleq1 rcla4v mtoi syl5bir syld tfis3 ) BDZU
      TEFZGZCDZVCEFZGZAAEFZGBCABCHZUTVCVAVDVGIUTVCEJKUTALZUTAVAVFVHIUTAEJKUTMNZ
      VECUTOVCVANZCUTOZVBVIVEVJCUTVICBPZQZVDVANZVEVJRVIVLVNVCUTSTVMVEVNVJVMVEVN
      VJVMVCMNZVEVNQVJRZUTVCUAVOVAMNZVPUTUBZVCVDVAUCUDUEUFUGUHUIVIVBVAUTNZUJZVK
      VIVQVBVTUKVRUTVAULUDVKVSVAVANZVAVRUMVJWACVAUTVCVAVAUNUOUPUQURUS $.
      $([?]$)

    $( Compute the aleph corresponding to a given transfinite cardinal number
       ` A ` . $)
    cardalephlem $p |- ( ( om (_ A /\ ( card ` A ) = A ) ->
                    ( |^| { x e. On | A (_ ( aleph ` x ) } e. On /\
                  A = ( aleph ` |^| { x e. On | A (_ ( aleph ` x ) } ) ) ) $=
      ( vy com wss ccrd cfv wceq wa cv cale con0 crab cint wcel cardon eleq1
      mpbii wrex alephle fveq2 sseq2d rcla4ev mpian onintrab2 sylib syl adantl
      c0 csuc wlim wo word eloni w3o ordzsl 3orass bitr 3syl wi ax-17 hbrab1
      hbint hbfv hbss onminsb a1i aleph0 syl6eq sseq1d biimprd im2and eqss
      syl6ibr comm ancoms wn weq onnminsb visset sucid eleq2 mpbiri syl5 imp
      csdm wbr alephsuc sylan9eqr eleq2d biimpd cdom breq2 cvv wb fvex domtri
      mpan2 sylibrd carddom sseq1 alephcard sseq2i syl6bb sylibd sylan9r mtod
      exp32 r19.23adv onelon sylan adantld mpcom alephon onelss nsyl nrexdv
      adantr ciun alephlim eliun mtbird exp jaod onsseleq mpbid ord syld mpd
      jca ) DBEZBFGZBHZIZBAJZKGZEZALMZNZLOZBYSKGZHZYMYTYKYMBLOZYTYMYLLOUVCBPYLB
      LQRZUVCYQALSZYTUVCBBKGZEZUVEBTYQUVGABLYOBHYPUVFBYOBKUAUBUCUDZYQAUEUFZUGUH
      YNYSUIHZYSCJZUJZHZCLSZYSUKZULZULZUVBYMUVQYKYMUVCYTUVQUVDUVIYTYSUMZUVQYSUN
      UVRUVJUVNUVOUOUVQCYSUPUVJUVNUVOUQURUFUSUHYNUVJUVBUVPYMYKUVJUVBUTUVJYMYKIZ
      UVBUVJUVSBUVAEZUVABEZIUVBUVJYMUVTYKUWAYMUVTUTUVJYMUVCUVEUVTUVDUVHYQUVTAAC
      BUVAUVKBOAVAACYSKUVKKOAVAACYRYQACLVBVCVDVEYOYSHYPUVABYOYSKUAUBVFZUSVGUVJU
      WAYKUVJUVADBUVJUVAUIKGDYSUIKUAVHVIVJVKVLBUVAVMVNVOVPYMUVPUVBUTYKYMUVPBUVA
      OZVQZUVBYMUVNUWDUVOYMUVMUWDCLYMUVKLOZUVMUWDYMUWEUVMIZIUWCBUVKKGZEZUWFUWHV
      QZYMUWEUVMUWIUWEUVKYSOZUWIUVMYQUWHAUVKACVRYPUWGBYOUVKKUAUBVSZUVMUWJUVKUVL
      OUVKCVTWAYSUVLUVKWBWCWDWEUHUWFUWCBUWGYOWFWGZALMNZOZYMUWHUWFUWCUWNUWFUVAUW
      MBUVMUWEUVAUVLKGUWMYSUVLKUAAUVKWHWIWJWKYMUWNYLUWGFGZEZUWHYMUVCUWNUWPUTUVD
      UVCUWNBUWGWLWGZUWPUVCUWNUWGBWFWGZVQZUWQUWLUWRABYOBUWGWFWMVSUVCUWGWNOZUWQU
      WSWOUVKKWPZBUWGLWNWQWRWSUVCUWTUWPUWQWOUXABUWGLWNWTWRWSUGYMUWPBUWOEUWHYLBU
      WOXAUWOUWGBUVKXBXCXDXEXFXGXHXIYMUVCUVOUWDUTUVDUVCUVOUWDUVCUVOIZUWCBUWGOZC
      YSSZUVCUXDVQUVOUVCUXCCYSUVCUWJIZUWHUXCUWEUXEUWIYTUWJUWEUVCYSUVKXJUVIXKUWE
      UWJUWIUVCUWKXLXMUWGBUVKXNXOXPXQXRUXBUWCBCYSUWGXSZOUXDUXBUVAUXFBYTUVOUVAUX
      FHUVCCYSLXTUVIXKWJCBYSUWGYAXDYBYCUGYDYMUVCUWDUVBUTUVDUVCUWCUVBUVCUVTUWCUV
      BULZUVCUVEUVTUVHUWBUGUVCUVALOUVTUXGWOYSXNBUVAYEWRYFYGUGYHUHYDYIYJ $.
      $([?]$)

    $( Every transfinite cardinal is an aleph and vice-versa.  Theorem 8A(b) of
       Enderton p. 213 and its converse. $)
    cardaleph $p |- ( om (_ A ->
                   ( ( card ` A ) = A <-> E. x e. On A = ( aleph ` x ) ) ) $=
      ( vy com wss ccrd cfv wceq cv cale con0 wrex wa crab cint wcel
      cardalephlem fveq2 cleq2d rcla4ev syl exp wi alephcard id cleq12d mpbiri
      a1i r19.23aiv impbid ) DBEZBFGZBHZBAIZJGZHZAKLZUKUMUQUKUMMBCIJGECKNOZKPBU
      RJGZHZMUQCBQUPUTAURKUNURHUOUSBUNURJRSTUAUBUQUMUCUKUPUMAKUPUMUCUNKPUPUMUOF
      GZUOHUNUDUPULVABUOBUOFRUPUEUFUGUHUIUHUJ $.
      $([?]$)
  $}

  ${
    $d x A $.
    $( Two ways of expressing the property of being a transfinite cardinal. $)
    isinfcard $p |- ( ( om (_ A /\ ( card ` A ) = A ) <-> A e. ran aleph ) $=
      ( vx cv con0 wcel cale cfv wceq wa wex wrex crn com wss ccrd df-rex
      cleqcom birex bitr3 wfn wb alephfnon fvelrn ax-mp cardaleph pm5.32i
      sseq2 alephgeom biimp syl5bir comm r19.23aiv pm4.71ri bitr4 3bitr4r ) BCZ
      DEZUPFGZAHZIBJZAURHZBDKZAFLEZMANZAOGAHZIZUTUSBDKVBUSBDPUSVABDURAQRSFDTVCU
      TUAUBBDAFUCUDVFVDVBIVBVDVEVBBAUEUFVBVDVAVDBDVAUQVDVAVDMURNZUQAURMUGUQVGUP
      UHUIUJUKULUMUNUO $.
      $([?]$)
  $}

  $( Two ways of expressing the property of being a cardinal number. $)
  iscard3 $p |- ( ( card ` A ) = A <-> A e. ( om u. ran aleph ) ) $=
    ( ccrd cfv wceq com wcel cale crn wo cun wn wss con0 word cardon eleq1
    mpbii eloni ordom ordtri2or mpan2 3syl ord wa isinfcard biimp exp comm
    syld orrd cardnn bicom pm3.27bd jaoi impbi elun bitr4 ) ABCZADZAEFZAGHZFZIZ
    AEVAJFUSVCUSUTVBUSUTKEALZVBUSUTVDUSAMFZANZUTVDIZUSURMFVEAOURAMPQARVFENVGSAE
    TUAUBUCVDUSVBVDUSVBVDUSUDZVBAUEZUFUGUHUIUJUTUSVBAUKVBVDUSVHVBVIULUMUNUOAEVA
    UPUQ $.
    $([?]$)

  $( The class of transfinite cardinals (the range of the aleph function) is a
     proper class. $)
  alephprc $p |- -. ran aleph e. V $=
    ( vx cv ccrd cfv wceq cab cvv wcel cale crn cardprc com wa cun unexbg
    iscard3 bicom biabri eleq1i bitr2 omex mpbiran mtbi ) ABZCDUDEZAFZGHZIJZGHZ
    AKUGLGHZUIUJUIMLUHNZGHUGLUHOUKUFGUEAUKUEUDUKHUDPQRSTUAUBUC $.
    $([?]$)

  $( The class of transfinite cardinals (the range of the aleph function) is a
     subset of the class of ordinal numbers. $)
  alephsson $p |- ran aleph (_ On $=
    ( vx cale crn con0 cv wcel com wss ccrd cfv wceq wa isinfcard cardon eleq1
    mpbii adantl sylbir ssriv ) ABCZDAEZTFGUAHZUAIJZUAKZLUADFZUAMUDUEUBUDUCDFUE
    UANUCUADOPQRS $.
    $([?]$)

  $( The union of the class of transfinite cardinals (the range of the aleph
     function) is the class of ordinal numbers. $)
  unialeph $p |- U. ran aleph = On $=
    ( cale crn cuni con0 wcel wn wceq cvv alephprc zfunb mtbi elisset mto word
    wo wss alephsson ssorduni ax-mp ordeleqon mpbi ori ) ABZCZDEZFUDDGZUEUDHEZU
    CHEUGIUCJKUDDLMUEUFUDNZUEUFOUCDPUHQUCRSUDTUAUBS $.
    $([?]$)

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function for
       positive arguments.  Theorem 64 of [Suppes] p. 229. $)
    alephval2 $p |- ( ( A e. On /\ (/) e. A ) -> ( aleph ` A ) =
                    |^| { x e. On | A. y e. A ( aleph ` y ) ~< x } ) $=
      ( vz cale cfv cv csdm wbr wral con0 crab wcel wn cint wceq c0 wa wss wi
      ssrab oneqmini ax-mp alephordi r19.21aiv alephon jctil breq2 biraldv
      elrab sylibr adantr com cdom wo cvv omex visset entri3 mp2an ccrd wrex
      wb alephord ancoms breq1 cen cardid sdomen1 syl5rbbr sylan9bb weq fveq2
      breq1d rcla4v comm sdomirr sdomen2 syl5bbr mtbiri nsyli adantl sylbird
      exp31 r19.23adv cardcard cardaleph mpbii syl5 carddom cardom sseq1i
      bitr3 syl5ib noel2 r19.2z domtr alephgeom ssdomg sylbi sylan5 domnsym
      syl onelon exp32 imp rexnal syl6ib exp imp3a a1d com3r jaod mpi onel
      cardsdomel alephcard eleq2i syl6rbb ibi pm3.27bd con3 syl6 sylan13 ) CEFZ
      BGZEFZAGZHIZBCJZAKLZMZDGZYKMZNZDYEJZYEYKOPZCKMZQCMZRZYKKSYLYPRYQTYJAKUADY
      EYKUBUCYRYLYSYRYEKMZYGYEHIZBCJZRYLYRUVCUVAYRUVBBCYFCUDUECUFZUGYJUVCAYEKYH
      YEPYIUVBBCYHYEYGHUHUIUJUKULYTYODYEYTYMYEMZYGYMHIZBCJZNZYOYTYMYEHIZUVHUVEY
      TUMYMUNIZYMUMUNIZUOZUVIUVHTZUMUPMZYMUPMZUVLUQDURZUMYMUPUPUSUTYTUVJUVMUVKY
      RUVJUVMTYSYRUMYMVAFZSZUVMUVJYRUVQYHEFZPZAKVBZUVMUVRYRUVTUVMAKYRYHKMZUVTUV
      MYRUWBRZUVTRUVIYHCMZUVHUWCUWDUVSYEHIZUVTUVIUWBYRUWDUWEVCYHCVDVEUVTUVQYEHI
      ZUWEUVIUVQUVSYEHVFUVOUVQYMVGIZUWFUVIVCUVPYMVHZUVQYMYEUPVIUTVJVKUVTUWDUVHT
      UWCUWDUVTUVHUWDUVGUVSYMHIZUVTUVGUWDUWIUVFUWIBYHCBAVLYGUVSYMHYFYHEVMVNVOVP
      UVTUWIUVSUVSHIZUVSVQUVTUVSUVQHIZUWJUWIUVQUVSUVSHUHUVOUWGUWKUWIVCUVPUWHUVQ
      YMUVSUPVRUTVSVTWAVPWBWCWDWEUVRUVQVAFUVQPUWAYMWFAUVQWGWHWIUVJUMVAFZUVQSZUV
      RUVNUVOUWMUVJVCUQUVPUMYMUPUPWJUTUWLUMUVQWKWLWMWNULUVKUVIYTUVHUVKYTUVHTUVI
      UVKYRYSUVHUVKYRYSUVHTYSUVKYRRZUVHYSUWNUVFNZBCVBZUVHYSCQPNZUWNUWPTCQWOUWQU
      WOBCJUWPUWNUWOBCWPUWNUWOBCUVKYRYFCMZUWOTUVKYRUWRUWOUVKYFKMZUWOYRUWRRUVKUW
      SRYMYGUNIZUWOUVKUMYGUNIZUWTUWSYMUMYGWQUWSUMYGSZUXAYFWRUVNUXBUXATUQUMYGUPW
      SUCWTXAYMYGXBXCCYFXDXAXEXFUEWIXCUVFBCXGXHVPXIXJXKXLXMXNUVEUVIUVEYMKMZUVEU
      VIVCYEYMUVDXOUXCUVIYMYEVAFZMUVEYMYEXPUXDYEYMCXQXRXSXCXTWIYNUVGYNUXCUVGYJU
      VGAYMKADVLYIUVFBCYHYMYGHUHUIUJYAYBYCUEYD $.
      $([?]$)
  $}

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function: it is
       the least infinite cardinal different from all values at smaller
       arguments.  Definition of aleph in [Enderton] p. 212 and definition
       of aleph in [BellMachover] p. 490 . $)
    alephval3 $p |- ( A e. On -> ( aleph ` A ) = |^| { x | ( ( card ` x ) = x
                   /\ om (_ x /\ A. y e. A -. x = ( aleph ` y ) ) } ) $=
      ( vz cale cfv cv ccrd wceq com wss wn wral w3a cab wcel cint con0 wa wi
      cardon eleq1 mpbii adantr 3adant2 ssab2i abid2 sseqtr oneqmini ax-mp
      alephcard a1i alephgeom biimp alephord2i alephon oneirr eleq2 mtbiri
      con2 syl6 r19.21aiv 3jca fvex fveq2 id cleq12d sseq2 cleq1 binegd
      biraldv bi3and elab sylibr wrex wex alephord2 bicomd sylan9bbr biimpcd
      pm3.27 jcad exp4c com3r imp4b 19.22dv df-rex 3imtr4g cardaleph biimpac
      syl5 imp dfrex2 sylib nan mpbir exp visset weq df-3an bitr bineg syl6ibr
      sylan13 ) CEFZAGZHFZXFIZJXFKZXFBGZEFZIZLZBCMZNZAOZPZDGZXPPZLZDXEMZXEXPQIZ
      CRPZXPRKXQYASYBTXPXFRPZAORXOYDAXHXNYDXIXHYDXNXHXGRPYDXFUAXGXFRUBUCUDUEUFA
      RUGUHDXEXPUIUJYCXEHFZXEIZJXEKZXEXKIZLZBCMZNZXQYCYFYGYJYFYCCUKULYCYGCUMUNY
      CYIBCYCXJCPZXKXEPZYIXJCUOYHYMYHYMXKXKPXKXJUPUQXEXKXKURUSUTVAVBVCXOYKAXECE
      VDXFXEIZXHYFXIYGXNYJYNXGYEXFXEXFXEHVEYNVFVGXFXEJVHYNXMYIBCYNXLYHXFXEXKVIV
      JVKVLVMVNYCXTDXEYCXRXEPZXRHFZXRIZJXRKZSZXRXKIZLZBCMZSZLZXTYCYOUVDYCYOSZUV
      DTUVEYSSZUVBLZTUVFYTBCVOZUVGUVEYSUVHUVEYTBRVOZUVHYSUVEXJRPZYTSZBVPYLYTSZB
      VPUVIUVHUVEUVKUVLBYCYOUVJYTUVLYOUVJYCYTUVLTYOUVJYCYTUVLYOUVJYCSZYTSZYLYTU
      VNYOYLYTYOYMUVMYLXRXKXEUBUVMYLYMXJCVQVRVSVTUVNYTTYOUVMYTWAULWBWCWDWEWFYTB
      RWGYTBCWGWHYRYQUVIBXRWIWJWKWLYTBCWMWNUVEYSUVBWOWPWQXSUVCXSYQYRUVBNZUVCXOU
      VOAXRDWRADWSZXHYQXIYRXNUVBUVPXGYPXFXRXFXRHVEUVPVFVGXFXRJVHUVPXMUVABCUVPXL
      YTXFXRXKVIVJVKVLVMYQYRUVBWTXAXBXCVBXD $.
      $([?]$)
  $}

