$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equivalence relations and classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Er $.  $( Equivalence predicate $)
  $c [ $. $( Left bracket $)
  $c ] $.  $( Right bracket $)
  $c /. $.  $( Long slash for quotient set $)

  $( Extend the definition of a wff to include the equivalence predicate. $)
  wer $a wff Er R $.

  $( Extend the definition of a class to include equivalence class. $)
  cec $a class ( R [ A ] ) $.

  $( Extend the definition of a class to include quotient set. $)
  cqs $a class ( A /. R ) $.

  $( Define equivalence predicate.  ` R ` need not be a relation but ordinarily
     will be, in which case we call it an equivalence relation.  Our
     notation is not standard.  Most literature uses informal English and
     may also require that ` R ` be a relation. $)
  df-er $a |- ( Er R <-> ( `' R u. ( R o. R ) ) (_ R ) $.

  ${
    $d x y z R $.
    $( Alternate definition of equivalence predicate. $)
    er2 $p |- ( Er R <-> A. x A. y A. z
              ( ( x R y -> y R x ) /\  ( ( x R y /\ y R z ) -> x R z ) ) ) $=
      ( wer cv wbr wi wal wa ccnv ccom cun wss df-er unss cnvsym cotr bi2an
      3bitr ax-17 19.28 bial 19.26 bitr bitr4 ) DEZAFZBFZDGZUIUHDGHZBIZAIZUJUIC
      FZDGJUHUNDGHZCIZBIZAIZJZUKUOJCIZBIZAIZUGDKZDDLZMDNVCDNZVDDNZJUSDOVCVDDPVE
      UMVFURABDQABCDRSTVBULUQJZAIUSVAVGAVAUKUPJZBIVGUTVHBUKUOCUKCUAUBUCUKUPBUDU
      EUCULUQAUDUEUF $.
      $([3-Jan-97]$)
  $}

  $( Define the ` R `-coset of ` A `.  Exercise 35 of Enderton, p. 61.  This is
     called the equivalence class of ` A ` modulo ` R ` when ` R ` is an
     equivalence relation.  Enderton (p. 57) uses the notation ` [ A ] _ R `
     which is somewhat nicer but requires that ` R ` be a subscript
     and would complicate Metamath's typesetting. $)
  df-ec $a |- ( R [ A ] ) = ( R " { A } ) $.

  ${
    $d y A x $.  $d y R x $.  $( ` x ` is dummy. $)
    ec2.1 $e |- A e. V $.
    $( Alternate definition of ` R `-coset of ` A `.  Definition 34 of Suppes,
       p. 81. $)
    ec2   $p |- ( R [ A ] ) = { y | A R y } $=
      ( vx csn cima cv wcel cop wa wex cab cec wbr dfima2 df-ec wceq opeq1
      cleq3d ceqsexv elsn biran biex df-br 3bitr4r biab 3eqtr4 ) CBFZGEHZUIIZU
      JAHZJZCIZKZELZAMBCNBULCOZAMEACUIPBCQUQUPAUJBRZUNKZELBULJZCIZUPUQUNVAEBDUR
      UMUTCUJBULSTUAUOUSEUKURUNEBUBUCUDBULCUEUFUGUH $.
      $([3-Jan-97]$)
  $}

  $( An equivalence class modulo a set is a set. $)
  ecexg   $p |- ( R e. B -> ( R [ A ] ) e. V ) $=
      cR cB wcel cR cA csn cima cvv wcel cA cR cec cvv wcel cR cA csn cB imaexg
      cA cR cec cR cA csn cima cvv cA cR df-ec cleq3i sylibr $.
      $([24-Jul-95]$)

  ${
    $d x y A $.  $d x y R $.
    $( Define quotient set.  ` R ` is usually an equivalence relation.
       Definition of Enderton, p. 58. $)
    df-qs $a |- ( A /. R ) = { y | E. x e. A y = ( R [ x ] ) } $.
  $}

  $( Equality theorem for equivalence predicate. $)
  ereq $p |- ( R = S -> ( Er R <-> Er S ) ) $=
      ( wceq ccnv ccom cun wss wer cnveq uneq1 syl coeq1 coeq2 eqtrd uneq2
      sseq1d sseq2 bitrd df-er 3bitr4g ) ABCZADZAAEZFZAGZBDZBBEZFZBGZAHBHUAUE
      UHAGUIUAUDUHAUAUDUFUCFZUHUAUBUFCUDUJCABIUBUFUCJKUAUCUGCUJUHCUAUCBAEUGABAL
      ABBMNUCUGUFOKNPABUHQRASBST $.
      $([4-Jun-95]$)

  ${
    $d x y z R $.
    ster.1 $e |- ( x R y -> y R x ) $.
    ster.2 $e |- ( ( x R y /\ y R z ) -> x R z ) $.
    $( A symmetric, transitive relation is an equivalence relation. $)
    ster   $p |- Er R $=
      ( wer cv wbr wi wa wal er2 pm3.2i ax-gen mpgbir ) DGAHZBHZDIZRQDIJZSRCHZD
      IKQUADIJZKZCLZBLAABCDMUDBUCCTUBEFNOOP $.
      $([4-Jun-95]$)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    ersym.1 $e |- A e. V $.
    ersym.2 $e |- B e. V $.
    ersym.3 $e |- Er R $.
    $( An equivalence relation is symmetric. $)
    ersym   $p |- ( A R B -> B R A ) $=
      ( vx vy vz cv wbr wi wceq wa breq12 wb ancoms bi2imd wal wer er2 mpbi
      a4i pm3.26i vtoclb2v ) GJZHJZCKZUGUFCKZLZABCKZBACKZLGHABDEUFAMZUGBMZNUHUK
      UIULUFAUGBCOUNUMUIULPUGBUFACOQRUJUHUGIJZCKNUFUOCKLZUJUPNZIUQISZHURHSZGCTU
      SGSGHICUAFUBUCUCUCUDUE $.
      $([4-Jun-95]$)
  $}

  ${
    ersymb.1 $e |- A e. V $.
    ersymb.2 $e |- B e. V $.
    ersymb.3 $e |- Er R $.
    $( An equivalence relation is symmetric. $)
    ersymb   $p |- ( A R B <-> B R A ) $=
      ( wbr ersym impbi ) ABCGBACGABCDEFHBACEDFHI $.
      $([30-Jul-95]$)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
       $( ` x y z` are dummy. $)
    ertr.1 $e |- A e. V $.
    ertr.2 $e |- B e. V $.
    ertr.3 $e |- C e. V $.
    ertr.4 $e |- Er R $.
    $( An equivalence relation is transitive. $)
    ertr   $p |- ( ( A R B /\ B R C ) -> A R C ) $=
      ( vx vy vz cv wbr wa wi wceq breq1 birand bi2imd breq2 bi2and birimd
      biland 3sylan9b wal wer er2 mpbi a4i pm3.27i vtoclb3v ) ILZJLZDMZUMKLZDM
      ZNZULUODMZOZABDMZBCDMZNZACDMZOZIJKABCEFGULAPZUSAUMDMZUPNZAUODMZOUMBPZUTBU
      ODMZNZVHOUOCPZVDVEUQVGURVHVEUNVFUPULAUMDQRULAUODQSVIVGVKVHVIVFUTUPVJUMBAD
      TUMBUODQUAUBVLVKVBVHVCVLVJVAUTUOCBDTUCUOCADTSUDUNUMULDMOZUSVMUSNZKVNKUEZJ
      VOJUEZIDUFVPIUEIJKDUGHUHUIUIUIUJUK $.
      $([4-Jun-95]$)
  $}

  ${
    $d x y A $.  $d x y R $.  $( ` x y ` are dummy. $)
    erref.1 $e |- Er R $.
    $( An equivalence relation is reflexive on its field.  Compare
       Theorem 3M, p. 56 of Enderton. $)
    erref   $p |- ( A e. ( dom R u. ran R ) -> A R A ) $=
      ( vx vy cv wbr cdm crn cun wceq breq1 breq2 bitrd wcel wo elun cop wex
      visset eldm df-br ertr ersym sylan5 anabss sylbir 19.23aiv sylbi elrn
      sylan jaoi vtoclga ) DFZUNBGZAABGZDABHZBIZJZUNAKUOAUNBGUPUNAUNBLUNAABMNUN
      USOUNUQOZUNUROZPUOUNUQURQUTUOVAUTUNEFZRBOZESUOEUNBDTZUAVCUOEVCUNVBBGZUOUN
      VBBUBVEUOVEVBUNBGZUOVEUNVBUNBVDETZVDCUCZUNVBBVDVGCUDUEUFUGUHUIVAVBUNRBOZE
      SUOEUNBVDUJVIUOEVIVFUOVBUNBUBVFUOVEVFUOVFVHVBUNBVGVDCUDUKUFUGUHUIULUIUM
      $.
      $([4-Jun-95]$)
  $}

  ${
    $d x y R $.  $( ` x y ` are dummy. $)
    erdmrn.1 $e |- Er R $.
    $( The range and domain of an equivalence relation are equal. $)
    erdmrn   $p |- dom R = ran R $=
      ( vx vy cdm crn cv cop wcel wex wbr visset ersymb df-br 3bitr3 biex eldm
      elrn 3bitr4 cleqri ) CAEZAFZCGZDGZHAIZDJUDUCHAIZDJUCUAIUCUBIUEUFDUCUDAKUD
      UCAKUEUFUCUDACLZDLBMUCUDANUDUCANOPDUCAUGQDUCAUGRST $.
      $([6-Aug-95]$)
  $}

  $( Equality theorem for equivalence class. $)
  eceq1 $p |- ( A = B -> ( A [ C ] ) = ( B [ C ] ) ) $=
      ( wceq csn cima cec imaeq1 df-ec 3eqtr4g ) ABDACEZFBKFCAGCBGABKHCAICBIJ
      $.
      $([23-Jul-95]$)

  $( Equality theorem for equivalence class. $)
  eceq2 $p |- ( A = B -> ( C [ A ] ) = ( C [ B ] ) ) $=
      ( wceq csn cima cec sneq imaeq2 syl df-ec 3eqtr4g ) ABDZCAEZFZCBEZFZACGBC
      GMNPDOQDABHNPCIJACKBCKL $.
      $([23-Jul-95]$)

  ${
    $d x A $.  $d x B $.  $d x R $.  $( ` x ` is dummy. $)
    elec.1 $e |- A e. V $.
    elec.2 $e |- B e. V $.
    $( Membership in an equivalence class.  Theorem 72 of Suppes, p. 82. $)
    elec   $p |- ( A e. ( R [ B ] ) <-> B R A ) $=
      ( vx cv wbr cec breq2 ec2 vtoclab ) BFGZCHBACHFABCIDMABCJFBCEKL $.
      $([23-Jul-95]$)
  $}

  ${
    $d x R $.  $d x A $.  $( ` x ` is dummy. $)
    ecdmn0.1 $e |- A e. V $.
    $( An equivalence class is not empty in its domain. $)
    ecdmn0   $p |- ( A e. dom R <-> -. ( R [ A ] ) = (/) ) $=
      ( vx cv cec wcel wex cop c0 wceq wn cdm wbr visset elec df-br bitr biex
      nnull eldm 3bitr4r ) DEZABFZGZDHAUCIBGZDHUDJKLABMGUEUFDUEAUCBNUFUCABDOCPA
      UCBQRSDUDTDABCUAUB $.
      $([15-Feb-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $( ` x ` is dummy. $)
    erthi.1 $e |- A e. V $.
    erthi.2 $e |- B e. V $.
    erthi.3 $e |- Er R $.
    $( Basic property of equivalence relations.  Part of Lemma 3N, p. 57 of
       Enderton. $)
    erthi   $p |- ( A R B -> ( R [ A ] ) = ( R [ B ] ) ) $=
      ( vx wbr cec cv wcel wi ersym visset ertr exp syl impbid elec 3bitr4g
      cleqrd ) ABCHZGACIZBCIZUBAGJZCHZBUECHZUEUCKUEUDKUBUFUGUBBACHZUFUGLABCDEF
      MUHUFUGBAUECEDGNZFOPQUBUGUFABUECDEUIFOPRUEACUIDSUEBCUIESTUA $.
      $([23-Jul-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $( ` x ` is dummy. $)
    erth.1 $e |- B e. V $.
    erth.2 $e |- Er R $.
    $( Basic property of equivalence relations.  Theorem 73, p. 82 of
       Suppes. $)
    erth   $p |- ( A e. ( dom R u. ran R ) ->
                   ( ( R [ A ] ) = ( R [ B ] ) <-> A R B ) ) $=
      ( vx cv cec wceq wbr wb cdm crn cun eceq2 cleq1d breq1 bi2bid wcel wi
      erref cleq4 visset elec 3bitr3g ersym syl6bi comm syl erthi a1i impbid
      vtoclga ) FGZCHZBCHZIZUNBCJZKACHZUPIZABCJZKFACLCMNZUNAIZUQUTURVAVCUOUSUPU
      NACOPUNABCQRUNVBSZUQURVDUNUNCJZUQURTUNCEUAUQVEURUQVEBUNCJZURUQUNUOSUNUPSV
      EVFUOUPUNUBUNUNCFUCZVGUDUNBCVGDUDUEBUNCDVGEUFUGUHUIURUQTVDUNBCVGDEUJUKULU
      M $.
      $([23-Jul-95]$)
  $}

  ${
    erthdm.1 $e |- B e. V $.
    erthdm.2 $e |- Er R $.
    $( Basic property of equivalence relations.  Compare Theorem 73, p. 82 of
       Suppes.  Requires membership in domain instead of field. $)
    erthdm   $p |- ( A e. dom R -> ( ( R [ A ] ) = ( R [ B ] ) <-> A R B ) ) $=
      ( cdm wcel crn cun cec wceq wbr wb elun1 erth syl ) ACFZGAQCHZIGACJBCJKAB
      CLMAQRNABCDEOP $.
      $([28-Jul-95]$)
  $}

  ${
    erthdmr.1 $e |- A e. V $.
    erthdmr.2 $e |- B e. V $.
    erthdmr.3 $e |- Er R $.
    $( Basic property of equivalence relations.  Compare Theorem 73, p. 82 of
       Suppes.  Requires membership of 2nd argument in domain. $)
    erthdmr  $p |- ( B e. dom R -> ( ( R [ A ] ) = ( R [ B ] ) <-> A R B ) ) $=
      ( cdm wcel cec wceq wbr erthdm cleqcom ersymb 3bitr4g ) BCGHBCIZACIZJBACK
      QPJABCKBACDFLQPMABCDEFNO $.
      $([30-Jul-95]$)
  $}

  ${
    ereldm.1 $e |- A e. V $.
    ereldm.2 $e |- B e. V $.
    ereldm.3 $e |- Er R $.
    ereldm.4 $e |- dom R = D $.
    $( Equality of equivalence classes implies equivalence of domain
       membership. $)
    ereldm   $p |- ( ( R [ A ] ) = ( R [ B ] ) -> ( A e. D <-> B e. D ) ) $=
      ( cec wceq cdm wcel wbr erthdm biimpcd ersym breldm syl syl6 erthdmr
      impbid cleq4i 3bitr3g ) ADIBDIJZADKZLZBUELZACLBCLUDUFUGUDUFABDMZUGUFUDUH
      ABDFGNOUHBADMUGABDEFGPBADFQRSUDUGUHUFUGUDUHABDEFGTOABDEQSUAUECAHUBUECBHUB
      UC $.
      $([28-Jan-96]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Equality theorem for quotient set. $)
    qseq1 $p |- ( A = B -> ( A /. C ) = ( B /. C ) ) $=
      ( vy vx wceq cv cec wrex cab cqs rexeq biabdv df-qs 3eqtr4g ) ABFZDGEGCH
      FZEAIZDJQEBIZDJACKBCKPRSDQEABLMEDACNEDBCNO $.
      $([23-Jul-95]$)

    $( Equality theorem for quotient set. $)
    qseq2 $p |- ( A = B -> ( C /. A ) = ( C /. B ) ) $=
      ( vy vx wceq cv cec wrex cab cqs eceq1 cleq2d birexdv biabdv df-qs
      3eqtr4g ) ABFZDGZEGZAHZFZECIZDJSTBHZFZECIZDJCAKCBKRUCUFDRUBUEECRUAUDSABTL
      MNOEDCAPEDCBPQ $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $( ` y ` is dummy. $)
    elqs.1  $e |- B e. V $.
    $( Membership in a quotient set. $)
    elqs  $p |- ( B e. ( A /. R ) <-> E. x ( x e. A /\ B = ( R [ x ] ) ) ) $=
      ( vy cqs wcel cv cec wceq wrex wa wex cleq1 birexdv df-qs vtoclab
      df-rex bitr ) CBDGZHCAIZDJZKZABLZUBBHUDMANFIZUCKZABLUEFCUAEUFCKUGUDABUFCU
      COPAFBDQRUDABST $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $( ` y ` is dummy. $)
    $( Membership in a quotient set. $)
    elqsi  $p |- ( B e. ( A /. R ) -> E. x ( x e. A /\ B = ( R [ x ] ) ) ) $=
      ( vy cv wcel cec wceq wa wex cqs cleq1 biland biexdv visset elqs biimp
      vtoclga ) AFZBGZEFZTDHZIZJZAKZUACUCIZJZAKECBDLZUBCIZUEUHAUJUDUGUAUBCUCMNO
      UBUIGUFABUBDEPQRS $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $( ` x y ` are dummy. $)
    ecelqsi.1 $e |- R e. V $.
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsi   $p |- ( B e. A -> ( R [ B ] ) e. ( A /. R ) ) $=
      ( vy vx cv cec cqs wcel wceq eceq2 cleq3d wa wex weq wi cleq3 cleq2d
      bi2and biimprcd cleqid mpan2 19.22dv a9e mpi cvv ecexg ax-mp elqs
      sylibr vtoclga ) EGZCHZACIZJZBCHZUOJEBAUMBKUNUQUOUMBCLMUMAJZFGZAJZUNUSCHZ
      KZNZFOZUPURFEPZFOVDURVEVCFURUNUNKZVEVCQVEVCURVFNVEUTURVBVFUSUMARVEVAUNUNU
      SUMCLSTUAUNUBUCUDFEUEUFFAUNCCUGJUNUGJUMUGCUHDUIUJUKUL $.
      $([25-Jul-95]$)
  $}

  ${
    ecopqsi.1 $e |- R e. V $.
    ecopqsi.2 $e |- S = ( ( A X. A ) /. R ) $.
    $( "Closure" law for equivalence class of ordered pairs. $)
    ecopqsi   $p |- ( ( B e. A /\ C e. A ) -> ( R [ <. B , C >. ] ) e. S ) $=
      ( wcel wa cop cxp cec opelxpi cqs ecelqsi cleq4i sylibr syl ) BAHCAHIBCJZ
      AAKZHZSDLZEHZBCAAMUAUBTDNZHUCTSDFOEUDUBGPQR $.
      $([25-Mar-96]$)
  $}

  ${
    $d x y A $.  $d x y R $.  $( ` x y ` are dummy. $)
    qsex.1 $e |- A e. V $.
    $( A quotient set exists. $)
    qsex   $p |- ( A /. R ) e. V $=
      ( vx vy cqs cv wcel cec wceq wa copab crn cvv wrex cab wex df-rex biab
      df-qs rnopab 3eqtr4 cdm wfun funrnex dmopab 19.42v eqtr zfausab eqeltr
      wmo funopab moeq moani mpgbir mp2 ) ABFZDGZAHZEGURBIZJZKZDELZMZNVADAOZEPV
      BDQZEPUQVDVEVFEVADARSDEABTVBDEUAUBVCUCZNHVCUDZVDNHNVCUEVGUSVAEQZKZDPZNVGV
      BEQZDPVKVBDEUFVLVJDUSVAEUGSUHVIDACUIUJVHVBEUKDVBDEULVAUSEEUTUMUNUOUPUJ $.
      $([14-Aug-95]$)
  $}

  ${
    $d x R $.  $d x A $.  $( ` x ` is dummy. $)
    0nelqs.1 $e |- Er R $.
    0nelqs.2 $e |- dom R = A $.
    $( A quotient set doesn't contain the empty set. $)
    0nelqs   $p |- -. (/) e. ( A /. R ) $=
      ( vx c0 cqs wcel cv cec wceq wa wex elqsi wn wi imnan cdm visset ecdmn0
      cleq4i cleqcom bineg 3bitr3 biimp mpbi nex mto ) FABGHEIZAHZFUIBJZKZLZEME
      AFBNUMEUJULOZPUMOUJULQUJUNUIBRZHUKFKZOUJUNUIBESTUOAUIDUAUPULUKFUBUCUDUEUF
      UGUH $.
      $([24-Aug-95]$)
  $}

  ${
    ecelqsdm.1 $e |- B e. V $.
    ecelqsdm.2 $e |- Er R $.
    ecelqsdm.3 $e |- dom R = A $.
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsdm   $p |- ( ( R [ B ] ) e. ( A /. R ) -> B e. A ) $=
      ( wcel cec cqs wn c0 wceq wb cdm ecdmn0 cleq4i bitr3 bicon1 cleq3 sylbi
      0nelqs mtbiri a3i ) BAGZBCHZACIZGZUDJZUGKUFGZUHUEKLZUGUIMUJUDUJJBCNZGUDBC
      DOUKABFPQRUEKUFSTACEFUAUBUC $.
      $([30-Jul-95]$)
  $}

  ${
    $d y A $.  $( ` y ` is dummy. $)
    ecid.1 $e |- A e. V $.
    $( A set is equal to its converse epsilon coset.  (Note: converse epsilon
       is not an equivalence relation. $)
    ecid   $p |- ( `' E [ A ] ) = A $=
      ( vy cv cep ccnv wbr cab wcel cec visset brcnv epelc bitr biab ec2 abid2
      3eqtr4 ) ACDZEFZGZCHSAIZCHATJAUAUBCUASAEGUBASEBCKZLSAUCBMNOCATBPCAQR $.
      $([13-Aug-95]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    qsid.1 $e |- A e. V $.
    $( A set is equal to its quotient set mod converse epsilon. $)
    qsid   $p |- ( A /. `' E ) = A $=
      ( vy vx cv cep ccnv cec wceq wrex cab wcel cqs weq visset ecid cleq2i
      eqcomb bitr birex risset bitr4 biab df-qs abid2 3eqtr4 ) CEZDEZFGZHZIZDAJ
      ZCKUGALZCKAUIMAULUMCULDCNZDAJUMUKUNDAUKCDNUNUJUHUGUHDOPQCDRSTDUGAUAUBUCDC
      AUIUDCAUEUF $.
      $([13-Aug-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $d x ps $.
    ectocl.1 $e |- S = ( B /. R ) $.
    ectocl.2 $e |- ( ( R [ x ] ) = A -> ( ph <-> ps ) ) $.
    ectocl.3 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of class for equivalence class. $)
    ectocl   $p |- ( A e. S -> ps ) $=
      ( wcel cqs cleq4i cv cec wceq wa wex elqsi wb cleqcoms syl5bi comm imp
      19.23aiv syl sylbi ) DGKDEFLZKZBGUHDHMUICNZEKZDUJFOZPZQZCRBCEDFSUNBCUKUMB
      UMUKBUMABUKABTULDIUAJUBUCUDUEUFUG $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z ps $.
      $( ` z ` is dummy. $)
    ecoptocl.1 $e |- S = ( ( B X. C ) /. R ) $.
    ecoptocl.2 $e |- ( ( R [ <. x , y >. ] ) = A -> ( ph <-> ps ) ) $.
    ecoptocl.3 $e |- ( ( x e. B /\ y e. C ) -> ph ) $.
    $( Implicit substitution of class for equivalence class of ordered pair. $)
    ecoptocl   $p |- ( A e. S -> ps ) $=
      ( vz wcel cxp cqs cleq4i cv cec wceq wa wex elqsi cop wi cleqid eceq2
      cleq2d birimd wb cleqcoms syl5bi comm optocl imp 19.23aiv syl sylbi )
      EINEFGOZHPZNZBIUTEJQVAMRZUSNZEVBHSZTZUAZMUBBMUSEHUCVFBMVCVEBECRZDRZUDZHSZ
      TZBUEVEBUECDVBFGUSUSUFVIVBTZVKVEBVLVJVDEVIVBHUGUHUIVKVGFNVHGNUAZBVKABVMAB
      UJVJEKUKLULUMUNUOUPUQUR $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.  $d x y z w D $.
    $d z w S $.  $d x y z w R $.  $d x y ps $.  $d z w ch $.
    2ecoptocl.1 $e |- S = ( ( C X. D ) /. R ) $.
    2ecoptocl.2 $e |- ( ( R [ <. x , y >. ] ) = A -> ( ph <-> ps ) ) $.
    2ecoptocl.3 $e |- ( ( R [ <. z , w >. ] ) = B -> ( ps <-> ch ) ) $.
    2ecoptocl.4 $e |- ( ( ( x e. C /\ y e. D ) /\
                      ( z e. C /\ w e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    2ecoptocl   $p |- ( ( A e. S /\ B e. S ) -> ch ) $=
      ( wcel wi cv cop cec wceq bilimd wa exp ecoptocl comm imp ) HMRZIMRZCUKU
      JCUJBSUJCSFGIJKLMNFTZGTZUALUBIUCBCUJPUDUJULJRUMKRUEZBUNASUNBSDEHJKLMNDTZE
      TZUALUBHUCABUNOUDUOJRUPKRUEUNAQUFUGUHUGUHUI $.
      $([23-Jul-95]$)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d v u C $.  $d x y z w v u D $.
    $d z w v u S $.  $d x y z w v u R $.  $d x y ps $.  $d z w ch $.
    $d v u th $.
    3ecoptocl.1 $e |- S = ( ( D X. D ) /. R ) $.
    3ecoptocl.2 $e |- ( ( R [ <. x , y >. ] ) = A -> ( ph <-> ps ) ) $.
    3ecoptocl.3 $e |- ( ( R [ <. z , w >. ] ) = B -> ( ps <-> ch ) ) $.
    3ecoptocl.4 $e |- ( ( R [ <. v , u >. ] ) = C -> ( ch <-> th ) ) $.
    3ecoptocl.5 $e |- ( ( ( x e. D /\ y e. D ) /\
                      ( z e. D /\ w e. D ) /\ ( v e. D /\ u e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    3ecoptocl   $p |- ( ( A e. S /\ B e. S /\ C e. S ) -> th ) $=
      ( wcel wa wi cv cop cec wceq bilimd 3exp imp3a ecoptocl comm 2ecoptocl
      exp3a 3imp ) KPUBZLPUBZMPUBZDUQURUSDURUSUCUQDUQBUDUQCUDUQDUDGHIJLMNNOPQGU
      EZHUEZUFOUGLUHBCUQSUIIUEZJUEZUFOUGMUHCDUQTUIUQUTNUBVANUBUCZVBNUBVCNUBUCZU
      CZBVFAUDVFBUDEFKNNOPQEUEZFUEZUFOUGKUHABVFRUIVGNUBVHNUBUCZVDVEAVIVDVEAUAUJ
      UKULUMUNUMUOUP $.
      $([9-Aug-95]$)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u S $.  $d x y H $.
    $d z w v u G $.  $d x y ph $.  $d z w v u ps $.
    brecop.1 $e |- S e. V $.
    brecop.2 $e |- Er S $.
    brecop.3 $e |- dom S = ( G X. G ) $.
    brecop.4 $e |- H = ( ( G X. G ) /. S ) $.
    brecop.5 $e |- R = { <. x , y >. | ( ( x e. H /\ y e. H ) /\
                      E. z E. w E. v E. u ( ( x = ( S [ <. z , w >. ] ) /\
                      y = ( S [ <. v , u >. ] ) ) /\ ph ) ) } $.
    brecop.6 $e |- ( ( ( ( z e. G /\ w e. G ) /\ ( A e. G /\ B e. G ) ) /\
                       ( ( v e. G /\ u e. G ) /\ ( C e. G /\ D e. G ) ) ) ->
                     ( ( ( S [ <. z , w >. ] ) = ( S [ <. A , B >. ] ) /\
                         ( S [ <. v , u >. ] ) = ( S [ <. C , D >. ] ) ) ->
                     ( ph <-> ps ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number
       construction. $)
    brecop   $p |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                ( ( S [ <. A , B >. ] ) R ( S [ <. C , D >. ] ) <-> ps ) ) $=
      ( wcel wa cop cec wbr cv wceq wex wb copab cleq3 birand cleq1 bi4exdv
      bi2and biland elopabg ibar bitr4d df-br cleq4i bitr syl5bb ecopqsi
      syl2an wi opeq12 eceq2 syl im2an an4s exp com13 imp cxp opex ereldm
      visset opelxp syl5bbr opelxpi syl5bir im2anan9 mpd pm5.74d cgsex4g
      cleqcom bi2an a1i biimtrut 3bitr4d bitrd ) IOUCJOUCUDZKOUCLOUCUDZUDZIJUEZ
      NUFZKLUEZNUFZMUGZWSEUHZFUHZUEZNUFZUIZXAGUHZHUHZUEZNUFZUIZUDZAUDZHUJGUJFUJ
      EUJZBWSPUCZXAPUCZXBXOUKWOWPXPXQUDZWSXAUEZCUHZPUCZDUHZPUCZUDZXTXFUIZYBXKUI
      ZUDZAUDZHUJGUJFUJEUJZUDZCDULZUCZXOXBXRYLXRXOUDZXOYJXPYCUDZXGYFUDZAUDZHUJG
      UJFUJEUJZUDYMCDWSXAPPXTWSUIZYDYNYIYQYRYAXPYCXTWSPUMUNYRYHYPEFGHYRYGYOAYRY
      EXGYFXTWSXFUOUNUNUPUQYBXAUIZYNXRYQXOYSYCXQXPYBXAPUMURYSYPXNEFGHYSYOXMAYSY
      FXLXGYBXAXKUOURUNUPUQUSXRXOUTVAXBXSMUCYLWSXAMVBMYKXSUAVCVDVEOIJNPQTVFOKLN
      PQTVFVGWQXFWSUIZXKXAUIZUDZWQAVHZUDZHUJGUJFUJEUJWQBVHZXOBUVCUVEUVBEFGHIJKL
      OOXCIUIXDJUIUDZYTXHKUIXILUIUDZUVAUVFXEWRUIYTXCXDIJVIXEWRNVJVKUVGXJWTUIUVA
      XHXIKLVIXJWTNVJVKVLUVBWQABUVBWQABUKZUVBWQUDXCOUCXDOUCUDZXHOUCXIOUCUDZUDZU
      VHUVBWQUVKUVHVHUVKWQUVBUVHUVKWQUVBUVHVHZUVIWOUVJWPUVLUBVMVNVOVPUVBWQUVKYT
      WOUVIUVAWPUVJYTUVIWROOVQZUCZWOYTXEUVMUCUVNUVIXEWRUVMNXCXDVRIJVRRSVSXCXDOO
      FVTWAWBIJOOWCWDUVAUVJWTUVMUCZWPUVAXJUVMUCUVOUVJXJWTUVMNXHXIVRKLVRRSVSXHXI
      OOHVTWAWBKLOOWCWDWEVPWFVNWGWHWQXNUVDEFGHWQXMUVBAUVCXMUVBUKWQXGYTXLUVAWSXF
      WIXAXKWIWJWKWQAWLUQUPWQBWLWMWN $.
      $([29-Jan-96]$)
  $}

  ${
    $v Q $.
    brecop2.cQ $f class Q $.
    brecop2.1 $e |- S e. V $.
    brecop2.2 $e |- B e. V $.
    brecop2.3 $e |- C e. V $.
    brecop2.4 $e |- D e. V $.
    brecop2.5 $e |- Er S $.
    brecop2.6 $e |- dom S = ( G X. G ) $.
    brecop2.7 $e |- H = ( ( G X. G ) /. S ) $.
    brecop2.8 $e |- R (_ ( H X. H ) $.
    brecop2.9 $e |- Q (_ ( G X. G ) $.
    brecop2.10 $e |- -. (/) e. G $.
    brecop2.11 $e |- dom F = ( G X. G ) $.
    brecop2.12 $e |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                    ( ( S [ <. A , B >. ] ) R ( S [ <. C , D >. ] ) <->
                    ( A F D ) Q ( B F C ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number
       construction.  Eliminates antecedent from last hypothesis. $)
    brecop2   $p |- ( ( S [ <. A , B >. ] ) R ( S [ <. C , D >. ] ) <->
                    ( A F D ) Q ( B F C ) ) $=
      ( wcel wa cop cec wbr co wb cvv ecexg ax-mp ndmbr cxp cqs cleq4i opex
      ecelqsdm sylbi opelxp sylib im2an syl oprex ndmoprrcl an41r3 pm5.21ni
      pm2.61i ) AHUCZBHUCZUDZCHUCZDHUCZUDZUDZABUEZFUFZCDUEZFUFZEUGZADGUHZBCGUHZ
      JUGZUIUBVTVOWCVTVQIUCZVSIUCZUDVOVQVSIIEFUJUCVSUJUCVRUJFUKKULRUMWDVKWEVNWD
      VPHHUNZUCZVKWDVQWFFUOZUCWGIWHVQQUPWFVPFABUQOPURUSABHHLUTVAWEVRWFUCZVNWEVS
      WHUCWIIWHVSQUPWFVRFCDUQOPURUSCDHHNUTVAVBVCWCVIVMUDZVJVLUDZUDZVOWCWAHUCZWB
      HUCZUDWLWAWBHHJBCGVDSUMWMWJWNWKADHGNUATVEBCHGMUATVEVBVCVIVMVJVLVFVAVGVH
      $.
      $([13-Feb-96]$)
  $}

  ${
    $( Several theorems about equivalence relations of the kind used in
       construction of fractions and signed reals. $)

    $v t $.
    $v s $.
    $v r $.
    ecopopr.vt $f var t $.
    ecopopr.vs $f var s $.
    ecopopr.vr $f var r $.

    $d f g h t s r A $.  $d f g h t s r B $.  $d f g h t s r C $.
    $d f g h t s r D $.  $d x y z w v u f g h t s r F $.
    $d f g h t s r R $.
    $d x y z w v u f g h t s r S $.  $( ` f g h t s r ` are dummy. $)

    $( We will work with a relation of this form.  ` F ` is an operation. $)
    ecopopr.1 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\ y e. ( S X. S )
          ) /\ E. z E. w E. v E. u ( ( x = <. z , w >. /\ y = <. v , u >. ) /\
          ( z F u ) = ( w F v ) ) ) } $.

    ${
      $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
      $d x y z w v u D $.
      $( Express the relation in terms of the operation. $)
      ecopopreq $p |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $=
      ( cv co wceq wb wa opreq12 cleqan12d an41r3s opbrop ) COZFOZMPZDOZEOZMPZ
      QZGJMPZHIMPZQZABCDEFGHIJKLUDGQZUEJQZUGHQZUHIQZUJUMRUNUOSUPUQSUFUKUIULUDGU
      EJMTUGHUHIMTUAUBNUC $.
      $([16-Aug-95]$)
    $}

    ${
      $( Assume the operation is commutative. $)
      ecopopr.com $e |- ( x F y ) = ( y F x ) $.
      $( Domain of the relation. $)
      ecopoprdm $p |- dom R = ( S X. S ) $=
      ( vf vg cdm cxp wss ssdm cv wcel wa cop wceq co wex copab opabssxp
      eqsstr ax-mp dmxpid sseqtr wi wal wrel wb relss relxp visset opelxp wbr
      ecopopreq anabss caoprcom mpbiri df-br sylib opex opeldm syl sylbi
      ax-gen mpgbir eqssi ) GNZHHOZVMVNVNOZNZVNGVOPVMVPPGVOQGARZVNSBRZVNSTVQCRZ
      DRZUAUBVRERZFRZUAUBTVSWBIUCVTWAIUCUBTFUDEUDDUDCUDZTABUEVOJWCABVNVNUFUGUHV
      NUIUJVNVMPZLRZMRZUAZVNSZWGVMSZUKZMULZLVNUMWDWKLULUNLMVNVMUOHHUPUHWJMWHWEH
      SWFHSTZWIWEWFHHMUQZURWLWGWGUAGSZWIWLWGWGGUSZWNWLWOWEWFIUCWFWEIUCUBZWLWOWP
      UNABCDEFWEWFWEWFGHIJUTVAABWEWFILUQWMKVBVCWGWGGVDVEWGWGGWEWFVFVGVHVIVJVKVL
      $.
      $([27-Aug-95]$)

      ${
        ecopopr.2 $e |- B e. V $.
        $( The relation is symmetric. $)
        ecopoprsym   $p |- ( A R B -> B R A ) $=
      ( vf vg vh ecopopr.vt wbr wi wb ibib cxp wcel wa cv cop wceq co wex
      copab opabssxp eqsstr brel cleqid breq1 breq2 bi2bid ecopopreq visset
      caoprcom cleq12i cleqcom bitr syl6bb ancoms bitr4d 2optocl syl mpbir )
      GHISZHGISZTVKVKVLUAZTVKVLUBVKGJJUCZUDHVNUDUEVMGHVNVNINIAUFZVNUDBUFZVNUDUE
      VOCUFZDUFZUGUHVPEUFZFUFZUGUHUEVQVTKUIVRVSKUIUHUEFUJEUJDUJCUJZUEABUKVNVNUC
      LWAABVNVNULUMUNOUFZPUFZUGZQUFZRUFZUGZISZWGWDISZUAGWGISZWGGISZUAVMOPQRGHJJ
      VNVNUOWDGUHWHWJWIWKWDGWGIUPWDGWGIUQURWGHUHWJVKWKVLWGHGIUQWGHGIUPURWBJUDWC
      JUDUEZWEJUDWFJUDUEZUEZWHWEWCKUIZWFWBKUIZUHZWIWNWHWBWFKUIZWCWEKUIZUHZWQABC
      DEFWBWCWEWFIJKLUSWTWPWOUHWQWRWPWSWOABWBWFKOUTRUTMVAABWCWEKPUTQUTMVAVBWPWO
      VCVDVEWMWLWIWQUAABCDEFWEWFWBWCIJKLUSVFVGVHVIVJ $.
      $([27-Aug-95]$)
      $}

      ${
        $( Assume the operation is closed. $)
        ecopopr.cl $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
        $( Assume the operation is associative. $)
        ecopopr.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
        $( Assume the operation has the cancellation property. $)
        ecopopr.can $e |- ( ( x e. S /\ y e. S ) ->
                   ( ( x F y ) = ( x F z ) -> y = z ) ) $.
        ${
          ecopopr.3 $e |- B e. V $.
          ecopopr.4 $e |- C e. V $.
          $( The relation is transitive. $)
          ecopoprtrn $p |- ( ( A R B /\ B R C ) -> A R C ) $=
      ( vf vg vh ecopopr.vt ecopopr.vs ecopopr.vr cxp wcel w3a wbr wa cv cop
      wi cleqid wceq breq1 birand bi2imd breq2 bi2and birimd biland co wb
      ecopopreq 3adant3 3adant1 opreq12 visset caopr411 caopr4 eqtr3 3eqtr4g
      syl6bi oprex caoprcan caoprcl syl2an 3impb 3com12 pm3.27 3sylan3 pm3.26
      3sylan1 syld 3adant2 sylibrd 3optocl wex copab opabssxp eqsstr brel
      pm3.26d im2an 3anass sylibr mpcom ) GKKUFZUGZHWSUGZIWSUGZUHZGHJUIZHIJUIZU
      JZGIJUIZTUKZUAUKZULZUBUKZUCUKZULZJUIZXMUDUKZUEUKZULZJUIZUJZXJXQJUIZUMGXMJ
      UIZXRUJZGXQJUIZUMXDHXQJUIZUJZYCUMXFXGUMTUAUBUCUDUEGHIKWSKWSUNXJGUOZXSYBXT
      YCYFXNYAXRXJGXMJUPUQXJGXQJUPURXMHUOZYBYEYCYGYAXDXRYDXMHGJUSXMHXQJUPUTVAXQ
      IUOZYEXFYCXGYHYDXEXDXQIHJUSVBXQIGJUSURXHKUGZXIKUGZUJZXKKUGXLKUGUJZXOKUGZX
      PKUGZUJZUHZXSXHXPLVCZXIXOLVCZUOZXTYPXSXKXLLVCZYQLVCZYTYRLVCZUOZYSYPXSXHXL
      LVCZXIXKLVCZUOZXKXPLVCZXLXOLVCZUOZUJZUVCYPXNUVFXRUVIYKYLXNUVFVDYOABCDEFXH
      XIXKXLJKLMVEVFYLYOXRUVIVDYKABCDEFXKXLXOXPJKLMVEVGUTUVJUVDUVGLVCUVEUVHLVCZ
      UVAUVBUVDUVEUVGUVHLVHABCXKXLXHXPLUBVIZUCVIZTVINPUEVIVJXIXLLVCXKXOLVCLVCUV
      BUVKABCXIXLXKXOLUAVIZUVMUVLNPUDVIZVJABCXIXLXKXOLUVNUVMUVLNPUVOVKVLVMVNYIY
      LYOUVCYSUMZYKYIYLYNUVPYOYLYIYNUVPYLYIYNUVPYTKUGYQKUGUVPYLYIYNUJABCYTYQYRK
      LXIXOLVOQVPABXKXLKLOVQABXHXPKLOVQVRVSVTYMYNWAWBYIYJWCWDWEYKYOXTYSVDYLABCD
      EFXHXIXOXPJKLMVEWFWGWHXFWTXAXBUJZUJXCXDWTXEUVQXDWTXAGHWSWSJRJAUKZWSUGBUKZ
      WSUGUJUVRCUKZDUKZULUOUVSEUKZFUKZULUOUJUVTUWCLVCUWAUWBLVCUOUJFWIEWIDWICWIZ
      UJABWJWSWSUFMUWDABWSWSWKWLZWMWNHIWSWSJSUWEWMWOWTXAXBWPWQWR $.
      $([11-Feb-96]$)
        $}

        $( The relation is an equivalence relation. $)
        ecopoprer $p |- Er R $=
      ( vf vg vh cv visset ecopoprsym ecopoprtrn ster ) OPQGABCDEFORZPRZGHIJKPS
      ZTABCDEFUCUDQRGHIJKLMNUEQSUAUB $.
      $([16-Feb-96]$)
      $}

    $}
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y A $.  $d x y B $.  $d x y C $.
    $d x y D $.
    eceqopreq.2 $e |- B e. V $.
    $( (We don't bother to eliminate this redundant hypothesis.) $)
    eceqopreq.3 $e |- C e. V $.
    eceqopreq.4 $e |- D e. V $.
    eceqopreq.5 $e |- Er R $.
    eceqopreq.6 $e |- dom R = ( S X. S ) $.
    eceqopreq.7 $e |- dom F = ( S X. S ) $.
    eceqopreq.8 $e |- -. (/) e. S $.
    eceqopreq.9 $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
    eceqopreq.10 $e |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $.
    $( Equality of equivalence relation in terms of an operation. $)
    eceqopreq $p |- ( ( A e. S /\ C e. S ) ->
                      ( ( R [ <. A , B >. ] ) = ( R [ <. C , D >. ] ) <->
                      ( A F D ) = ( B F C ) ) ) $=
      ( wcel wa cop cec wceq co wb wi w3a wbr cdm pm3.26 cxp opelxpi cleq4i
      sylibr opex erthdm 3syl bitrd exp43 3imp wn c0 cleq1 biimprcd pm2.16d
      ecdmn0 opelxp 3bitr3 pm3.27 sylbi nsyl4 syl5ibr comm 3adant3 cleq3
      caoprcl syl5bir ndmoprrcl pm3.27d syl6 3adant1 jcad pm5.21 pm2.61d
      3exp com23 imp biimpcd syl5bi pm3.26d 3adant2 cleq2 con1 con3 ndmopr
      syl pm5.1 a1i ) CHSZEHSZTZDHSZCDUAZGUBZEFUAZGUBZUCZCFIUDZDEIUDZUCZUEZWSWT
      XBXKUFWSXBWTXKWSXBWTXKWSXBWTUGZFHSZXKWSXBWTXMXKUFWSXBWTXMXKWSXBTZWTXMTZTZ
      XGXCXEGUHZXJXPXNXCGUIZSZXGXQUEXNXOUJXNXCHHUKZSZXSCDHHULXRXTXCNUMZUNXCXEGE
      FUOZMUPUQRURUSUTXLXMVAZXGVAZXJVAZTZXKXLYDYEYFWSXBYDYEUFWTYDXNYEYDXDVBUCZV
      AZYEXNXFVBUCZYIYEUFXMYJXGYHXGYHYJXDXFVBVCZVDVEYJVAZXOXMXEXRSXEXTSYLXOXRXT
      XENUMXEGYCVFEFHHLVGVHZWTXMVIVJZVKXSYAYIXNYBXCGCDUOVFCDHHJVGVHZVLVMVNXBWTY
      DYFUFWSXBWTTZXJXMXJYPXMXJYPXHHSZXMXJYQXIHSZYPXHXIHVOZABDEHIQVPVQYQWSXMCFH
      ILOPVRVSVTVMVEWAWBXGXJWCZVTWDWEWFWGXAXMXBVAZXKUFZWSWTXMUVBUFWSWTXMUVBWSWT
      XMUGZUVAYGXKUVCUVAYEYFWTXMUVAYEUFWSUVAXOYEUVAYLYEXOYHYLYEUFXBYHXGYJXGYHYJ
      YKWHVEYIXNXBYOWSXBVIVJZVKYMVLVMWAWSXMUVAYFUFWTWSXMTZXJXBXJUVEXBXJUVEYRXBX
      JYQYRUVEYSABCFHIQVPWIYRXBWTDEHIKOPVRWJVTVMVEWKWBYTVTWEWGYDUVBUFXAYDUVAXGX
      JTXKYDUVAXGXJYDXGYHUVAYJXGYHUEXMXFVBXDWLYNVKYHXBUVDWMVQUVAYDXJUVAYPVAXIVB
      UCZYDXJUFYPXBXBWTUJWNDEHIKOWOUVFXJXHVBUCZYDXIVBXHWLYDUVEVAUVGUVEXMWSXMVIW
      NCFHILOWOWPVQUQVMWBXGXJWQVTWRWDWD $.
      $([15-Feb-96]$)
  $}

  ${
    $d x y z w v u F $.
    $d x y z w v u R $.
    $d x y z w v u S $.
    $d x y z w v u A $.
    $d x y z w v u B $.  $( ` u ` is dummy. $)
    th3qlem1.1 $e |- Er R $.
    th3qlem1.2 $e |- dom R = S $.
    $( Compatibility assumption. $)
    th3qlem1.3 $e |- ( ( ( y e. S /\ w e. S ) /\ ( z e. S /\ v e. S ) ) ->
                 ( ( y R w /\ z R v ) -> ( y F z ) R ( w F v ) ) ) $.
    $( Lemma for Exercise 44 version of Theorem 3Q of Enderton, p. 60. $)
    th3qlem1 $p |- ( ( A e. ( S /. R ) /\ B e. ( S /. R ) ) -> E* x
       E. y E. z ( ( A = ( R [ y ] ) /\ B = ( R [ z ] ) ) /\
       x = ( R [ ( y F z ) ] ) ) ) $=
      ( vu cqs wcel wa cv cec wceq co wex weq wi wal wmo an4 bilan cleq3
      pm5.32ri bi2an bitr 3bitr wbr cdm wb pm3.26 cleq4i sylibr visset erthdm
      syl bi2anan9 sylbid cleq12 oprex erthi syl5bir syl9 com23 imp ecelqsdm
      im2an cleq1 biimpa syl2an an4s sylbi anabs sylanb exp imp3a syl5ib
      19.23advv ee4anv syl5ibr 19.21aivv biland bi2exdv eceq2 cleq2d opreq12
      bi2and cbvex2v syl6bb mo4 ) FIHOZPZGWQPZQZFBRZHSZTZGCRZHSZTZQZARZXAXDJUAZ
      HSZTZQZCUBBUBZFDRZHSZTZGERZHSZTZQZNRZXNXQJUAZHSZTZQZEUBDUBZQZANUCZUDZNUEA
      UEXMAUFWTYIANWTXLYEQZEUBDUBZCUBBUBYHYGWTYKYHBCWTYJYHDEWTXGXTQZXKYDQZQYHYJ
      WTYLYMYHWTYLYMYHUDZWRWRQZWSWSQZQZYLYNWTYQYLQZXBWQPZXCQZXOWQPZXPQZQZXEWQPZ
      XFQZXRWQPZXSQZQZQZYNYRYQXCXPQZXFXSQZQZQYOUVJQZYPUVKQZQUVIYLUVLYQXCXFXPXSU
      GUHYOYPUVJUVKUGUVMUVCUVNUVHUVMWRXCQZWRXPQZQUVCWRWRXCXPUGUVOYTUVPUVBXCWRYS
      FXBWQUIUJXPWRUVAFXOWQUIUJUKULUVNWSXFQZWSXSQZQUVHWSWSXFXSUGUVQUVEUVRUVGXFW
      SUVDGXEWQUIUJXSWSUVFGXRWQUIUJUKULUKUMUVIYSUVAQZUVJQZUVDUVFQZUVKQZQYNUVCUV
      TUVHUWBYSXCUVAXPUGUVDXFUVFXSUGUKUVSUWAUVJUVKYNXAIPZXNIPZQZXDIPZXQIPZQZQZX
      BXOTZXEXRTZQZYNUVSUWAQUVLUWIUWLYNUWIYMUWLYHUWIUWLXIYBHUNZYMYHUWIUWLXAXNHU
      NZXDXQHUNZQUWMUWEUWJUWNUWHUWKUWOUWEXAHUOZPZUWJUWNUPUWEUWCUWQUWCUWDUQUWPIX
      ALURUSXAXNHDUTZKVAVBUWHXDUWPPZUWKUWOUPUWHUWFUWSUWFUWGUQUWPIXDLURUSXDXQHEU
      TZKVAVBVCMVDYMYHXJYCTZUWMXHXJYAYCVEXIYBHXAXDJVFXNXQJVFKVGVHVIVJVKUVSUWEUW
      AUWHYSUWCUVAUWDIXAHBUTKLVLIXNHUWRKLVLVMUVDUWFUVFUWGIXDHCUTKLVLIXQHUWTKLVL
      VMVMUVJUWJUVKUWKXCXPUWJFXBXOVNVOXFXSUWKGXEXRVNVOVMVPVQVRVRWRYOWSYPWRVSWSV
      SUKVTWAWBXGXKXTYDUGWCWDWDXLYEBCDEWEWFWGXMYFANYHXMXGYAXJTZQZCUBBUBYFYHXLUX
      CBCYHXKUXBXGXHYAXJVNWHWIUXCYEBCDEBDUCZCEUCZQZXGXTUXBYDUXDXCXPUXEXFXSUXDXB
      XOFXAXNHWJWKUXEXEXRGXDXQHWJWKVCUXFXJYCYAUXFXIYBTUXAXAXNXDXQJWLXIYBHWJVBWK
      WMWNWOWPUS $.
      $([3-Aug-95]$)
  $}

  ${
    $v t $.
    $v s $.
    th3q.vt $f var t $.
    th3q.vs $f var s $.
    $d x y z w v u t s f g h R $.
    $d x y z w v u t s f g h S $.
    $d x y z w v u t s f g h A $.
    $d x y z w v u t s f g h B $.
    $d x y z w v u t s f g h C $.
    $d x y z w v u t s f g h D $.
    $d x y z w v u t s f g h F $.
    $d x y z w v u t s f g h G $.
    th3q.1 $e |- R e. V $.
    th3q.2 $e |- Er R $.
    th3q.3 $e |- dom R = ( S X. S ) $.
    $( Compatibility assumption. $)
    th3q.4 $e |- ( ( ( ( w e. S /\ v e. S ) /\ ( u e. S /\ t e. S ) )
       /\ ( ( s e. S /\ f e. S ) /\ ( g e. S /\ h e. S ) ) ) ->
       ( ( <. w , v >. R <. u , t >. /\ <. s , f >. R <. g , h >. ) ->
       ( <. w , v >. F <. s , f >. ) R ( <. u , t >. F <. g , h >. ) ) ) $.

    $( Lemma for Theorem 3Q of Enderton, p. 60, extended to operations on
       ordered pairs. $)
    th3qlem2 $p |- ( ( A e. ( ( S X. S ) /. R ) /\ B e. ( ( S X. S ) /. R ) )
           -> E* z E. w E. v E. u E. t (
           ( A = ( R [ <. w , v >. ] ) /\ B = ( R [ <. u , t >. ] ) ) /\
                  z = ( R [ ( <. w , v >. F <. u , t >. ) ] ) ) ) $=
      ( vx vy cxp cqs wcel wa cv cec wceq co wex wmo cop wbr wi cleqid breq1
      birand opreq1 breq1d bi2imd bilimd breq2 breq2d biland opreq2 exp
      comm 2optocl imp th3qlem1 immo opex eceq2 cleq2d bi2anan9 opreq12 syl
      bi2and cla4e2v 19.23aiaiv mpg ) EHHUAZGUBZUCFWBUCUDENUEZGUFZUGZFSUEZGUFZ
      UGZUDZAUEZWCWFLUHZGUFZUGZUDZSUINUIZAUJZEBUEZCUEZUKZGUFZUGZFDUEZMUEZUKZGUF
      ZUGZUDZWJWSXDLUHZGUFZUGZUDZMUIDUIZCUIBUIZAUJZANSITEFGWALPQWCWAUCIUEZWAUCU
      DWFWAUCTUEZWAUCUDZWCXOGULZWFXPGULZUDZWKXOXPLUHZGULZUMZXQWSXDGULZXSUDZWSWF
      LUHZXDXPLUHZGULZUMZUMXQWCXDGULZXSUDZWKYGGULZUMZUMXQYCUMBCDMWCXOHHWAWAUNZW
      SWCUGZYIYMXQYOYEYKYHYLYOYDYJXSWSWCXDGUOUPYOYFWKYGGWSWCWFLUQURUSUTXDXOUGZY
      MYCXQYPYKXTYLYBYPYJXRXSXDXOWCGVAUPYPYGYAWKGXDXOXPLUQVBUSUTXQWQHUCWRHUCUDX
      BHUCXCHUCUDUDZYIYQYDWCXOUKZJUEZKUEZUKZGULZUDZWSYRLUHZXDUVALUHZGULZUMZUMYQ
      YDWFUVAGULZUDZYFUVEGULZUMZUMYQYIUMNIJKWFXPHHWAYNYRWFUGZUVGUVKYQUVLUVCUVIU
      VFUVJUVLUVBUVHYDYRWFUVAGUOVCUVLUVDYFUVEGYRWFWSLVDURUSUTUVAXPUGZUVKYIYQUVM
      UVIYEUVJYHUVMUVHXSYDUVAXPWFGVAVCUVMUVEYGYFGUVAXPXDLVDVBUSUTYQWCHUCXOHUCUD
      YSHUCYTHUCUDUDZUVGYQUVNUVGRVEVFVGVFVGVHVIXMWOUMWPXNUMAXMWOAVJXLWOBCXKWODM
      WNXKNSWSXDWQWRVKXBXCVKWCWSUGZWFXDUGZUDZWIXGWMXJUVOWEXAUVPWHXFUVOWDWTEWCWS
      GVLVMUVPWGXEFWFXDGVLVMVNUVQWLXIWJUVQWKXHUGWLXIUGWCWSWFXDLVOWKXHGVLVPVMVQV
      RVSVSVTVP $.
      $([4-Aug-95]$)

      ${
        th3q.5 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( ( S X. S ) /. R )
           /\ y e. ( ( S X. S ) /. R ) ) /\ E. w E. v E. u E. t ( (
           x = ( R [ <. w , v >. ] ) /\ y = ( R [ <. u , t >. ] ) ) /\
           z = ( R [ ( <. w , v >. F <. u , t >. ) ] ) ) ) } $.

        $( Corollary of Theorem 3Q of Enderton, p. 60. $)
        th3qcor $p |- Fun G $=
      ( wfun cv cxp cqs wcel wa cop cec wceq co wex copab2 wb funeq ax-mp wmo
      wi moanv th3qlem2 mpbir funoprab ) MUAZAUBZHHUCGUDZUEBUBZVDUEUFZVCDUBEUBU
      GZGUHUIVEFUBNUBUGZGUHUIUFCUBVGVHLUJGUHUIUFNUKFUKEUKDUKZUFZABCULZUAZMVKUIV
      BVLUMMVKUNTUOVJABCVJCUPVFVICUPUQVFVICURCDEFVCVEGHIJKLNOPQRSUSUTVAUT $.
      $([12-Nov-95]$)

        $( Theorem 3Q of Enderton, p. 60, extended to operations on ordered
           pairs. $)
        th3q $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                 ( ( R [ <. A , B >. ] ) G ( R [ <. C , D >. ] ) ) =
                 ( R [ ( <. A , B >. F <. C , D >. ) ] ) ) $=
      ( cop cec cxp cqs wcel wa cv wceq co wex cvv ecexg ax-mp cleq1 birand
      bi4exdv biland th3qlem2 oprabvali opelxpi ecelqsi syl im2an wb opeq12
      eceq2 cleq2d opreq1 bi2and cla4e2gv opreq2 19.22dvv sylan9 cleqid
      pm3.2i mpi syl13 ) GHUEZKUFZLLUGZKUHZUIZIJUEZKUFZWEUIZUJWCDUKZEUKZUEZKUFZ
      ULZWHFUKZRUKZUEZKUFZULZUJZWBWGPUMZKUFZWLWQPUMZKUFZULZUJZRUNFUNZEUNDUNZWCW
      HQUMXBULGLUIHLUIUJZILUIJLUIUJZUJZAUKZWMULZBUKZWRULZUJZCUKZXDULZUJZRUNFUNE
      UNDUNWNXOUJZXRUJZRUNFUNEUNDUNWTXRUJZRUNFUNEUNDUNXHABCWCWHXBWEWEQKUOUIXBUO
      UIXAUOKUPTUQXLWCULZXSYADEFRYCXPXTXRYCXMWNXOXLWCWMURUSUSUTXNWHULZYAYBDEFRY
      DXTWTXRYDXOWSWNXNWHWRURVAUSUTXQXBULZYBXFDEFRYEXRXEWTXQXBXDURVAUTCDEFXLXNK
      LMNOPRSTUAUBUCVBUDVCXIWFXJWIXIWBWDUIWFGHLLVDWDWBKTVEVFXJWGWDUIWIIJLLVDWDW
      GKTVEVFVGXKWCWCULZWHWHULZUJZXBXBULZUJZXHXIYJWNYGUJZXBWLWGPUMZKUFZULZUJZEU
      NDUNXJXHYOYJDEGHLLWJGULWKHULUJWLWBULZYOYJVHWJWKGHVIYPYKYHYNYIYPWNYFYGYPWM
      WCWCWLWBKVJVKUSYPYMXBXBYPYLXAULYMXBULWLWBWGPVLYLXAKVJVFVKVMVFVNXJYOXGDEXF
      YOFRIJLLWOIULWPJULUJWQWGULZXFYOVHWOWPIJVIYQWTYKXEYNYQWSYGWNYQWRWHWHWQWGKV
      JVKVAYQXDYMXBYQXCYLULXDYMULWQWGWLPVOXCYLKVJVFVKVMVFVNVPVQYHYIYFYGWCVRWHVR
      VSXBVRVSVTWA $.
      $([4-Aug-95]$)
    $}
  $}

  ${
    $v s $.
    $v t $.
    $v a $.
    $v b $.
    $v c $.
    $v d $.
    $v J $.
    $v K $.
    $v L $.
    $v Q $.
    oprec.vs $f var s $.
    oprec.vt $f var t $.
    oprec.va $f var a $.
    oprec.vb $f var b $.
    oprec.vc $f var c $.
    oprec.vd $f var d $.
    oprec.cJ $f class J $.
    oprec.cK $f class K $.
    oprec.cL $f class L $.
    oprec.cQ $f class Q $.
    $d x y z w v u t s f g h a b c d A $.
    $d x y z w v u t s f g h a b c d B $.
    $d x y z w v u t s f g h a b c d C $.
    $d x y z w v u t s f g h a b c d D $.
    $d x y z t s g h a b c d F $.
    $d x y z t s g h a b c d G $.
    $d x y z w v u f H $.
    $d x y z J $.
    $d x y z w v u f K $.
    $d x y z w v u f L $.
    $d x y z a b c d Q $.
    $d x y z t s g h a b c d R $.
    $d x y z w v u t s f g h a b c d S $.
    $d x y ph $.
    $d z w v u ps $.
    $d z w v u ch $.
    oprec.1 $e |- H e. V $.
    oprec.2 $e |- K e. V $.
    oprec.3 $e |- L e. V $.
    oprec.4 $e |- R e. V $.
    oprec.5 $e |- Er R $.
    oprec.6 $e |- dom R = ( S X. S ) $.
    $( Equivalence relation. $)
    oprec.7 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. z E. w E. v E. u ( ( x = <. z , w >. /\
                      y = <. v , u >. ) /\ ph ) ) } $.
    $( Substitutions for equivalence relation. $)
    oprec.8 $e |- ( ( ( z = a /\ w = b ) /\ ( v = c /\ u = d ) ) ->
                     ( ph <-> ps ) ) $.
    oprec.9 $e |- ( ( ( z = g /\ w = h ) /\ ( v = t /\ u = s ) ) ->
                     ( ph <-> ch ) ) $.
    $( Pre-operation. $)
    oprec.10 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. w E. v E. u E. f ( ( x = <. w , v >. /\
                      y = <. u , f >. ) /\ z = J ) ) } $.
    $( Substitutions for pre-operation. $)
    oprec.11 $e |- ( ( ( w = a /\ v = b ) /\ ( u = g /\ f = h ) ) -> J = K ) $.
    oprec.12 $e |- ( ( ( w = c /\ v = d ) /\ ( u = t /\ f = s ) ) -> J = L ) $.
    oprec.13 $e |- ( ( ( w = A /\ v = B ) /\ ( u = C /\ f = D ) ) -> J = H ) $.
    $( Main operation. $)
    oprec.14 $e |- F = { <. <. x , y >. , z >. | ( ( x e. Q /\ y e. Q ) /\
           E. a E. b E. c E. d ( (
           x = ( R [ <. a , b >. ] ) /\ y = ( R [ <. c , d >. ] ) ) /\
           z = ( R [ ( <. a , b >. G <. c , d >. ) ] ) ) ) } $.
    $( Quotient class for main operation. $)
    oprec.15 $e |- Q = ( ( S X. S ) /. R ) $.
    $( Lemma to establish compatibility of pre-operation. $)
    oprec.16 $e |- ( ( ( ( a e. S /\ b e. S ) /\ ( c e. S /\ d e. S ) ) /\
                       ( ( g e. S /\ h e. S ) /\ ( t e. S /\ s e. S ) ) ) ->
                   ( ( ps /\ ch ) -> K R L ) ) $.
    $( Express operation on equivalence classes of ordered pairs in terms
       of equivalence class of operations on ordered pairs. $)
    oprec $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                ( ( R [ <. A , B >. ] ) F ( R [ <. C , D >. ] ) ) =
                   ( R [ H ] ) ) $=
      ( wcel wa cop cec co cv wbr opbrop bi2anan9 wb oprabval3 breqan12d an4s
      3imtr4d wceq wex copab2 cxp cqs cleq4i bi2an biran bioprab eqtr th3q
      eceq2 syl eqtrd ) JOVHKOVHVILOVHMOVHVIVIZJKVJZNVKLMVJZNVKSVLWQWRTVLZNVKZ
      UANVKZDEFUDUEUFJKLMNORUCUBTSUGQUOUPUQUDVMZOVHUEVMZOVHVIZUFVMZOVHUGVMZOVHV
      IZVIZQVMZOVHRVMZOVHVIZUCVMZOVHUBVMZOVHVIZVIZVIBCVIUIUJNVNZXBXCVJZXEXFVJZN
      VNZXIXJVJZXLXMVJZNVNZVIXQXTTVLZXRYATVLZNVNZVGXHXSBXOYBCABDEFGHIXBXCXEXFNO
      USURVOACDEFGHIXIXJXLXMNOUTURVOVPXDXKXGXNYEXPVQXDXKVIXGXNVIYCUIYDUJNDEFGHI
      XBXCXIXJUHUIPTOUMVBVAVRDEFGHIXEXFXLXMUHUJPTOUNVCVAVRVSVTWASDVMZUKVHZEVMZU
      KVHZVIZYFXQNVKWBYHXRNVKWBVIFVMXQXRTVLNVKWBVIUGWCUFWCUEWCUDWCZVIZDEFWDYFOO
      WENWFZVHZYHYMVHZVIZYKVIZDEFWDVEYLYQDEFYJYPYKYGYNYIYOUKYMYFVFWGUKYMYHVFWGW
      HWIWJWKWLWPWSUAWBWTXAWBDEFGHIJKLMUHUAPTOULVDVAVRWSUANWMWNWO $.
      $([6-Aug-95]$)
  $}

  ${
    $v J $.
    ecoprcom.cJ $f class J $.
    $d x y z w A $.  $d x y z w B $. $d x y z w F $.  $d x y z w R $.
    $d x y z w S $.  $d z w C $.
    ecoprcom.1 $e |- C = ( ( S X. S ) /. R ) $.
    $( D,G for AFB $)
    ecoprcom.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( ( R [ <. x , y >. ] ) F ( R [ <. z , w >. ] ) ) = ( R [ <. D , G >. ] ) ) $.
    $( H,J for BFA $)
    ecoprcom.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( x e. S /\ y e. S ) ) ->
 ( ( R [ <. z , w >. ] ) F ( R [ <. x , y >. ] ) ) = ( R [ <. H , J >. ] ) ) $.
    ecoprcom.4 $e |- D = H $.
    ecoprcom.5 $e |- G = J $.
    $( Lemma used in proving commutative laws via equivalence classes. $)
    ecoprcom $p |- ( ( A e. C /\ B e. C ) -> ( A F B ) = ( B F A ) ) $=
      ( cv cop cec co wceq opreq1 opreq2 cleq12d wcel wa opeq12 eceq2 syl
      mp2an syl6eq ancoms eqtr4d 2ecoptocl ) ATZBTZUAIUBZCTZDTZUAIUBZKUCZVCUTK
      UCZUDEVCKUCZVCEKUCZUDEFKUCZFEKUCZUDABCDEFJJIGOUTEUDVDVFVEVGUTEVCKUEUTEVCK
      UFUGVCFUDVFVHVGVIVCFEKUFVCFEKUEUGURJUHUSJUHUIZVAJUHVBJUHUIZUIZVDMNUAZIUBZ
      VEVLVDHLUAZIUBZVNPHMUDZLNUDZVPVNUDZVQVRUIVOVMUDVSHLMNUJVOVMIUKULRSUMUNVKV
      JVEVNUDQUOUPUQ $.
      $([29-Aug-95]$)
  $}

  ${
    $v J $.
    $v K $.
    $v L $.
    $v M $.
    $v N $.
    $v Q $.
    ecoprass.cJ $f class J $.
    ecoprass.cK $f class K $.
    ecoprass.cL $f class L $.
    ecoprass.cM $f class M $.
    ecoprass.cN $f class N $.
    ecoprass.cQ $f class Q $.
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d z w v u D $.
    ecoprass.1 $e |- D = ( ( S X. S ) /. R ) $.
    ecoprass.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( ( R [ <. x , y >. ] ) F ( R [ <. z , w >. ] ) ) = ( R [ <. G , H >. ] ) ) $.
    ecoprass.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( ( R [ <. z , w >. ] ) F ( R [ <. v , u >. ] ) ) = ( R [ <. N , Q >. ] ) ) $.
    ecoprass.4 $e |- ( ( ( G e. S /\ H e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( ( R [ <. G , H >. ] ) F ( R [ <. v , u >. ] ) ) = ( R [ <. J , K >. ] ) ) $.
    ecoprass.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( N e. S /\ Q e. S ) ) ->
 ( ( R [ <. x , y >. ] ) F ( R [ <. N , Q >. ] ) ) = ( R [ <. L , M >. ] ) ) $.
    ecoprass.6 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( G e. S /\ H e. S ) ) $.
    ecoprass.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( N e. S /\ Q e. S ) ) $.
    ecoprass.8 $e |- J = L $.
    ecoprass.9 $e |- K = M $.
    $( Lemma used in proving associative laws via equivalence classes. $)
    ecoprass $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( ( A F B ) F C ) = ( A F ( B F C ) ) ) $=
      ( cv cop cec co wceq opreq1 opreq1d cleq12d opreq2 opreq2d wcel wa w3a
      adantr sylan eqtrd 3impa opeq12 eceq2 syl mp2an syl6eq adantl sylan5
      3impb eqtr4d 3ecoptocl ) AUKZBUKZULKUMZCUKZDUKZULKUMZMUNZEUKZFUKZULKUMZMU
      NZVTWCWGMUNZMUNZUOGWCMUNZWGMUNZGWIMUNZUOGHMUNZWGMUNZGHWGMUNZMUNZUOWNIMUNZ
      GHIMUNZMUNZUOABCDEFGHILKJUBVTGUOZWHWLWJWMXAWDWKWGMVTGWCMUPUQVTGWIMUPURWCH
      UOZWLWOWMWQXBWKWNWGMWCHGMUSUQXBWIWPGMWCHWGMUPUTURWGIUOZWOWRWQWTWGIWNMUSXC
      WPWSGMWGIHMUSUTURVRLVAVSLVAVBZWALVAWBLVAVBZWELVAWFLVAVBZVCZWHRSULZKUMZWJX
      GWHPQULZKUMZXIXDXEXFWHXKUOXDXEVBZXFVBWHNOULKUMZWGMUNZXKXLWHXNUOXFXLWDXMWG
      MUCUQVDNLVAOLVAVBXFXNXKUOXLUEUGVEVFVGPRUOZQSUOZXKXIUOZXOXPVBXJXHUOXQPQRSV
      HXJXHKVIVJUIUJVKVLXDXEXFWJXIUOXDXEXFVBZVBWJVTTUAULKUMZMUNZXIXRWJXTUOXDXRW
      IXSVTMUDUTVMXDTLVAUALVAVBXTXIUOXRUFUHVNVFVOVPVQ $.
      $([31-Aug-95]$)
  $}

  ${
    $v J $.
    $v K $.
    $v L $.
    $v M $.
    $v N $.
    $v W $.
    $v X $.
    $v Y $.
    $v Z $.
    ecoprdist.cJ $f class J $.
    ecoprdist.cK $f class K $.
    ecoprdist.cL $f class L $.
    ecoprdist.cM $f class M $.
    ecoprdist.cN $f class N $.
    ecoprdist.cW $f class W $.
    ecoprdist.cX $f class X $.
    ecoprdist.cY $f class Y $.
    ecoprdist.cZ $f class Z $.
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d x y z w v u G $.  $d z w v u D $.
    ecoprdist.1 $e |- D = ( ( S X. S ) /. R ) $.
    $( M,N for BFC $)
    ecoprdist.2 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( ( R [ <. z , w >. ] ) F ( R [ <. v , u >. ] ) ) = ( R [ <. M , N >. ] ) ) $.
    $( H,J for AG(BFC) $)
    ecoprdist.3 $e |- ( ( ( x e. S /\ y e. S ) /\ ( M e. S /\ N e. S ) ) ->
 ( ( R [ <. x , y >. ] ) G ( R [ <. M , N >. ] ) ) = ( R [ <. H , J >. ] ) ) $.
    $( W,X for AGB $)
    ecoprdist.4 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( ( R [ <. x , y >. ] ) G ( R [ <. z , w >. ] ) ) = ( R [ <. W , X >. ] ) ) $.
    $( Y,Z for AGC $)
    ecoprdist.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( ( R [ <. x , y >. ] ) G ( R [ <. v , u >. ] ) ) = ( R [ <. Y , Z >. ] ) ) $.
    $( K,L for (AGB)F(AGC) $)
    ecoprdist.6 $e |- ( ( ( W e. S /\ X e. S ) /\ ( Y e. S /\ Z e. S ) ) ->
 ( ( R [ <. W , X >. ] ) F ( R [ <. Y , Z >. ] ) ) = ( R [ <. K , L >. ] ) ) $.
    ecoprdist.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( M e. S /\ N e. S ) ) $.
    ecoprdist.8 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( W e. S /\ X e. S ) ) $.
    ecoprdist.9 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( Y e. S /\ Z e. S ) ) $.
    ecoprdist.10 $e |- H = K $.
    ecoprdist.11 $e |- J = L $.
    $( Lemma used in proving distributive laws via equivalence classes. $)
    ecoprdist $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) ) $=
      ( cv cop cec co wceq opreq1 opreq12d cleq12d opreq2d opreq2 opreq1d wcel
      wa w3a adantl sylan5 eqtrd 3impb opeq12 eceq2 syl mp2an syl6eq
      opreqan12d syl2an 3impdi eqtr4d 3ecoptocl ) AUPZBUPZUQKURZCUPZDUPZUQKURZE
      UPZFUPZUQKURZMUSZNUSZWFWINUSZWFWLNUSZMUSZUTGWMNUSZGWINUSZGWLNUSZMUSZUTGHW
      LMUSZNUSZGHNUSZWTMUSZUTGHIMUSZNUSZXDGINUSZMUSZUTABCDEFGHILKJUEWFGUTZWNWRW
      QXAWFGWMNVAXJWOWSWPWTMWFGWINVAWFGWLNVAVBVCWIHUTZWRXCXAXEXKWMXBGNWIHWLMVAV
      DXKWSXDWTMWIHGNVEVFVCWLIUTZXCXGXEXIXLXBXFGNWLIHMVEVDXLWTXHXDMWLIGNVEVDVCW
      DLVGWELVGVHZWGLVGWHLVGVHZWJLVGWKLVGVHZVIZWNQRUQZKURZWQXPWNOPUQZKURZXRXMXN
      XOWNXTUTXMXNXOVHZVHWNWFSTUQKURZNUSZXTYAWNYCUTXMYAWMYBWFNUFVDVJXMSLVGTLVGV
      HYCXTUTYAUGUKVKVLVMOQUTZPRUTZXTXRUTZYDYEVHXSXQUTYFOPQRVNXSXQKVOVPUNUOVQVR
      XMXNXOWQXRUTXMXNVHZXMXOVHZVHWQUAUBUQKURZUCUDUQKURZMUSZXRYGYHWOYIWPYJMUHUI
      VSUALVGUBLVGVHUCLVGUDLVGVHYKXRUTYGYHUJULUMVTVLWAWBWC $.
      $([2-Sep-95]$)
  $}

