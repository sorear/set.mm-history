$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The union of a class and the Axiom of Union
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class union symbol. $)
  $c U. $. $( Big cup $)

  $( Extend class notation to include the union of a class (read:  'union
     ` A ` ') $)
  cuni $a class U. A $.

  ${
    $d x y A $.
    $( Define the union of a class.  Definition 5.5 of [TakeutiZaring]
       p. 16. $)
    df-uni $a |- U. A = { x | E. y ( x e. y /\ y e. A ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class union. $)
    dfuni2 $p |- U. A = { x | E. y e. A  x e. y } $=
      ( cuni wel cv wcel wa wex cab wrex df-uni exancom df-rex bitr4 biabi
      eqtr ) CDABEZBFCGZHBIZAJRBCKZAJABCLTUAATSRHBIUARSBMRBCNOPQ $.
      $( [28-Jun-98] $)
  $}

  ${
    $d x A y $.  $d x B y $.  $( ` y ` is dummy. $)
    $( Membership in class union. $)
    eluni $p |- ( A e. U. B <-> E. x ( A e. x /\ x e. B ) ) $=
      ( vy cuni wcel cvv cv wa wex elisset adantr 19.23aiv wel wceq eleq1
      birand biexdv df-uni elab2g pm5.21nii ) BCEZFBGFZBAHZFZUDCFZIZAJZBUBKUGUC
      AUEUCUFBUDKLMDANZUFIZAJUHDBUBGDHZBOZUJUGAULUIUEUFUKBUDPQRDACSTUA $.
      $( [22-May-94] $)

    $( Membership in class union.  Restricted quantifier version. $)
    eluni2 $p |- ( A e. U. B <-> E. x e. B A e. x ) $=
      ( cv wcel wa wex cuni wrex exancom eluni df-rex 3bitr4 ) BADZEZNCEZFAGPOF
      AGBCHEOACIOPAJABCKOACLM $.
      $( [31-Aug-99] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Membership in class union. $)
    elunii $p |- ( ( A e. B /\ B e. C ) -> A e. U. C ) $=
      ( vx wcel wa cv wex cuni wceq eleq2 eleq1 bi2and cla4egv anabsi7 eluni
      sylibr ) ABEZBCEZFZADGZEZUACEZFZDHZACIERSUEUDTDBCUABJUBRUCSUABAKUABCLMNOD
      ACPQ $.
      $( [24-Mar-95] $)
  $}

  ${
    $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbuni.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for union. $)
    hbuni $p |- ( y e. U. A -> A. x y e. U. A ) $=
      ( vz wel cv wcel wa wex wal cuni ax-17 hbel hban hbex eluni bial 3imtr4
      ) BEFZEGZCHZIZEJZUDAKBGZCLHZUFAKUCAETUBATAMZABBUACUGDNOPEUECQZUFUDAUHRS
      $.
      $( [30-Dec-96] $)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( Membership in union of a class abstract. $)
    eluniab $p |- ( A e. U. { x | ph } <-> E. x ( A e. x /\ ph ) ) $=
      ( vy cab cuni wcel cv wa wex eluni ax-17 hbab1 hban weq eleq2 eleq1
      bi2and abid bilan syl6bb cbvex bitr ) CABEZFGCDHZGZUEUDGZIZDJCBHZGZAIZBJD
      CUDKUHUKDBUFUGBUFBLABDMNUKDLDBOZUHUJUIUDGZIUKULUFUJUGUMUEUICPUEUIUDQRUMAU
      JABSTUAUBUC $.
      $( [11-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Equality theorem for class union.  Exercise 15 of [TakeutiZaring]
       p. 18. $)
    unieq $p |- ( A = B -> U. A = U. B ) $=
      ( vx vy wceq wel cv wcel wa wex cab cuni eleq2 biland biexdv biabdv
      df-uni 3eqtr4g ) ABEZCDFZDGZAHZIZDJZCKTUABHZIZDJZCKALBLSUDUGCSUCUFDSUBUET
      ABUAMNOPCDAQCDBQR $.
      $( [10-Aug-93] $)
  $}

  ${
    unieqi.1 $e |- A = B $.
    $( Inference of equality of two class unions. $)
    unieqi $p |- U. A = U. B $=
      ( wceq cuni unieq ax-mp ) ABDAEBEDCABFG $.
      $( [30-Aug-93] $)
  $}

  ${
    unieqd.1 $e |- ( ph -> A = B ) $.
    $( Deduction of equality of two class unions. $)
    unieqd $p |- ( ph -> U. A = U. B ) $=
      ( wceq cuni unieq syl ) ABCEBFCFEDBCGH $.
      $( [21-Apr-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    unpr.1 $e |- A e. V $.
    unpr.2 $e |- B e. V $.
    $( The union of a pair is the union of its members.  Proposition 5.7 of
       [TakeutiZaring] p. 16. $)
    unpr $p |- U. { A , B } = ( A u. B ) $=
      ( vx vy cv wcel wo cab wel cpr wa wex cun cuni wceq 19.43 visset elpr
      bilan andi bitr biex clel3 exancom bi2or 3bitr4r biabi df-un df-uni
      3eqtr4r ) EGZAHZUMBHZIZEJEFKZFGZABLZHZMZFNZEJABOUSPUPVBEUQURAQZMZUQURBQZM
      ZIZFNVDFNZVFFNZIVBUPVDVFFRVAVGFVAUQVCVEIZMVGUTVJUQURABFSTUAUQVCVEUBUCUDUN
      VHUOVIUNVCUQMFNVHFUMACUEVCUQFUFUCUOVEUQMFNVIFUMBDUEVEUQFUFUCUGUHUIEABUJEF
      USUKUL $.
      $( [23-Aug-93] $)
  $}

  $( The union of an ordered pair.  Theorem 65 of [Suppes] p. 39. $)
  unop $p |- U. <. A , B >. = { A , B } $=
    ( cop cuni csn cpr cun df-op unieqi snex prex unpr wss wceq snsspr ssequn1
    mpbi 3eqtr ) ABCZDAEZABFZFZDTUAGZUASUBABHITUAAJABKLTUAMUCUANABOTUAPQR $.
    $( [27-Aug-04] $) $( [17-Aug-04] $)

  ${
    unisn.1 $e |- A e. V $.
    $( A set equals the union of its singleton.  Theorem 8.2 of [Quine]
       p. 53. $)
    unisn $p |- U. { A } = A $=
      ( csn cuni cpr cun dfsn2 unieqi unpr unidm 3eqtr ) ACZDAAEZDAAFALMAGHAABB
      IAJK $.
      $( [30-Aug-93] $)
  $}

  ${
    $d x A $.
    $( A set equals the union of its singleton.  Theorem 8.2 of [Quine]
       p. 53. $)
    unisng $p |- ( A e. B -> U. { A } = A ) $=
      ( vx cv csn cuni wceq sneq unieqd id cleq12d visset unisn vtoclg ) CDZEZF
      ZOGAEZFZAGCABOAGZQSOATPROAHITJKOCLMN $.
      $( [13-Aug-02] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The class union of the union of two classes.  Theorem 8.3 of
       [Quine] p. 53. $)
    uniun $p |- U. ( A u. B ) = ( U. A u. U. B ) $=
      ( vx vy cun cuni wel cv wcel wa wex wo 19.43 elun bilan andi bitr biex
      eluni bi2or 3bitr4 cleqri ) CABEZFZAFZBFZEZCDGZDHZUCIZJZDKZCHZUEIZUMUFIZL
      ZUMUDIUMUGIUHUIAIZJZUHUIBIZJZLZDKURDKZUTDKZLULUPURUTDMUKVADUKUHUQUSLZJVAU
      JVDUHUIABNOUHUQUSPQRUNVBUOVCDUMASDUMBSTUADUMUCSUMUEUFNUAUB $.
      $( [20-Aug-93] $)

    $( The class union of the intersection of two classes.  Exercise 4.12(n)
       of [Mendelson] p. 235. $)
    uniin $p |- U. ( A i^i B ) (_ ( U. A i^i U. B ) $=
      ( vx vy cin cuni wel cv wcel wa wex 19.40 eluni elin bilan anandi bitr
      biex bi2an 3imtr4 ssriv ) CABEZFZAFZBFZEZCDGZDHZAIZJZUGUHBIZJZJZDKZUJDKZU
      LDKZJZCHZUCIZURUFIZUJULDLUSUGUHUBIZJZDKUNDURUBMVBUMDVBUGUIUKJZJUMVAVCUGUH
      ABNOUGUIUKPQRQUTURUDIZURUEIZJUQURUDUENVDUOVEUPDURAMDURBMSQTUA $.
      $( [4-Dec-03] $) $( [4-Dec-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Subclass relationship for class union.  Theorem 61 of [Suppes] p. 39. $)
    uniss $p |- ( A (_ B -> U. A (_ U. B ) $=
      ( vx vy wss wel cv wcel wa wex cab cuni wi wal ssel imland 19.22dv
      19.21aiv ss2ab sylibr df-uni 3sstr4g ) ABEZCDFZDGZAHZIZDJZCKZUDUEBHZIZDJZ
      CKZALBLUCUHULMZCNUIUMEUCUNCUCUGUKDUCUFUJUDABUEOPQRUHULCSTCDAUACDBUAUB $.
      $( [22-Mar-98] $)

    $( Subclass relationship for class union. $)
    ssuni $p |- ( ( A (_ B /\ B e. C ) -> A (_ U. C ) $=
      ( vx vy wss wcel cuni cv wi wceq sseq2 birimd wel wal wa wex 19.8a exp
      com12 eluni syl6ibr syl3d 19.20dv dfss2 3imtr4g vtoclga imp ) ABFZBCGZACH
      ZFZUJUIULADIZFZULJUIULJDBCUMBKUNUIULUMBALMUMCGZEIZAGZEDNZJZEOUQUPUKGZJZEO
      UNULUOUSVAEUOURUTUQUOURURUOPZDQZUTURUOVCURUOVCVBDRSTDUPCUAUBUCUDEAUMUEEAU
      KUEUFUGTUH $.
      $( [24-May-94] $)
  $}

  $( The union of the empty set is the empty set.  Theorem 8.7 of [Quine]
     p. 54. $)
  uni0 $p |- U. (/) = (/) $=
    ( c0 cuni wss wceq csn 0ss uniss ax-mp 0ex unisn sseqtr ss0 ) ABZACMADMAEZB
    ZAANCMOCNFANGHAIJKMLH $.
    $( [25-Mar-04] $) $( [16-Sep-93] $)

  ${
    $d x y A $.
    $( The union of a set is empty iff the set is included in the singleton of
       the empty set. $)
    uni0b $p |- ( U. A = (/) <-> A (_ { (/) } ) $=
      ( vx vy cv c0 csn wcel wral wceq wss cuni elsn biral dfss3 wn wex wrex
      n0 wel rexcom4 birex eluni2 biex 3bitr4r rexnal 3bitr bicon4 ) BDZEFZGZBA
      HUHEIZBAHZAUIJAKZEIZUJUKBABELMBAUINUNULUNOCDZUMGZCPZUKOZBAQZULOCUMRCBSZCP
      ZBAQUTBAQZCPUSUQUTBCATURVABACUHRUAUPVBCBUOAUBUCUDUKBAUEUFUGUD $.
      $( [13-Sep-04] $) $( [12-Sep-04] $)
  $}

  $( An element of a class is a subclass of its union.  Theorem 8.6 of [Quine]
     p. 54.  Also the basis for Proposition 7.20 of [TakeutiZaring] p. 40. $)
  elssuni $p |- ( A e. B -> A (_ U. B ) $=
    ( wss wcel cuni ssid ssuni mpan ) AACABDABECAFAABGH $.
    $( [6-Jun-94] $)

  ${
    $d x y A $.  $d x y B $.  $( ` y ` is dummy. $)
    $( Relationship involving membership, subset, and union.  Exercise 5
       of [Enderton] p. 26 and its converse. $)
    unissb $p |- ( U. A (_ B <-> A. x e. A x (_ B ) $=
      ( vy cuni wss cv wcel wi wal wral wel wa dfss2 wex eluni birim 19.23v
      bitr4 bial alcom 19.21v impexp bi2.04 bitr bilim 3bitr4 3bitr df-ral ) BEZ
      CFZAGZBHZULCFZIZAJZUNABKUKDGZUJHZUQCHZIZDJDALZUMMZUSIZAJZDJZUPDUJCNUTVDDU
      TVBAOZUSIVDURVFUSAUQBPQVBUSARSTVEVCDJZAJUPVCDAUAVGUOAUMVAUSIZIZDJUMVHDJZI
      VGUOUMVHDUBVCVIDVCVAUMUSIIVIVAUMUSUCVAUMUSUDUETUNVJUMDULCNUFUGTUEUHUNABUI
      S $.
      $( [20-Sep-03] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w B $.
    $( A subclass condition on the members of two classes that implies
       a subclass relation on their unions. $)
    uniss2 $p |- ( A. x e. A E. y e. B x (_ y -> U. A (_ U. B ) $=
      ( cv wss wrex wral cuni wcel ssuni exp com12 r19.23aiv r19.20si unissb
      sylibr ) AEZBEZFZBDGZACHRDIZFZACHCIUBFUAUCACTUCBDTSDJZUCTUDUCRSDKLMNOACUB
      PQ $.
      $( [4-Apr-04] $) $( [22-Mar-04] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w B $.
    $( If the difference ` A \ B ` contains the largest members of ` A ` ,
       then the union of the difference is the union of ` A ` . $)
    unidif $p |- ( A. x e. A E. y e. ( A \ B ) x (_ y ->
               U. ( A \ B ) = U. A ) $=
      ( cv wss cdif wrex wral cuni wa wceq uniss2 difss uniss ax-mp jctil eqss
      sylibr ) AEBEFBCDGZHACIZTJZCJZFZUCUBFZKUBUCLUAUEUDABCTMTCFUDCDNTCOPQUBUCR
      S $.
      $( [18-Apr-04] $) $( [22-Mar-04] $)
  $}

  $( The removal of the empty set from a class does not affect its union. $)
  unidif0 $p |- U. ( A \ { (/) } ) = U. A $=
    ( c0 csn cdif cuni cun uniun undif1 uncom eqtr2 unieqi 0ex unisn uneq2i
    un0 3eqtr4r uneq1i 3eqtr ) ABCZDZEZBAEZFZUBBFUBUASAFZEZSEZUBFUCTSFZEUAUFFZU
    EUATSGUDUGUGASFUDASHASIJKUHUABFUAUFBUABLMZNUAOJPSAGUFBUBUIQRBUBIUBOR $.
    $( [23-Mar-04] $) $( [22-Mar-04] $)

  ${
    $d x A $.  $d x B $.
    $( Relationship implying union. $)
    ssunieq $p |- ( ( A e. B /\ A. x e. B x (_ A ) -> A = U. B ) $=
      ( wcel cv wss wral wa cuni wceq elssuni unissb biimpr im2an eqss sylibr
      ) BCDZAEBFACGZHBCIZFZSBFZHBSJQTRUABCKUARACBLMNBSOP $.
      $( [10-Nov-99] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A set is a union of its subsets. $)
    unisseq $p |- ( A e. B -> A = U. { x e. B | x (_ A } ) $=
      ( vy wcel cv wss crab cuni wceq wa ssid jctr sseq1 elrab sylibr wral
      pm3.27bd rgen ssunieq mpan2 syl ) BCEZBAFZBGZACHZEZBUFIJZUCUCBBGZKUGUCUIB
      LMUEUIABCUDBBNOPUGDFZBGZDUFQUHUKDUFUJUFEUJCEUKUEUKAUJCUDUJBNORSDBUFTUAUB
      $.
      $( [13-Aug-02] $)
  $}

  ${
    $d x y z w A $.  $( ` x y z w ` are dummy. $)
    uniex.1 $e |- A e. V $.
    $( The ZF Axiom of Union in class notation.   This says that if ` A ` is
       a set i.e. ` A e. V ` , then the union of ` A ` is also a set.
       Same as Axiom 3 of [TakeutiZaring] p. 16. $)
    uniex $p |- U. A e. V $=
      ( vz vx vy cv cuni cvv wcel wceq unieq eleq1d wex wel wb wal wi wa axun
      eluni birim bial biex mpbir bm1.3ii dfcleq issetri vtocl ) CFZGZHIAGZHICA
      BUIAJUJUKHUIAKLDUJDFZUJJZDMEDNZEFZUJIZOEPZDMUPDEUPUNQZEPZDMUNDCNRDMZUNQZE
      PZDMDECSUSVBDURVAEUPUTUNDUOUITUAUBUCUDUEUMUQDEULUJUFUCUDUGUH $.
      $( [11-Aug-93] $)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The ZF Axiom of Union in class notation, in the form of a theorem
       instead of an inference.  We use the antecedent ` A e. B ` instead of
       ` A e. V ` to make the theorem more general and thus shorten some
       proofs; obviously ` V ` is one possibility for ` B ` . $)
    uniexg $p |- ( A e. B -> U. A e. V ) $=
      ( vx cv cuni cvv wcel wceq unieq eleq1d visset uniex vtoclg ) CDZEZFGAEZF
      GCABNAHOPFNAIJNCKLM $.
      $( [25-Nov-94] $)
  $}

  ${
    unex.1 $e |- A e. V $.
    unex.2 $e |- B e. V $.
    $( The union of two sets is a set.  Corollary 5.8 of [TakeutiZaring]
        p. 16. $)
    unex $p |- ( A u. B ) e. V $=
      ( cpr cuni cun cvv unpr prex uniex eqeltrr ) ABEZFABGHABCDIMABJKL $.
      $( [1-Jul-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Existence of union is equivalent to existence of its components. $)
    unexb $p |- ( ( A e. V /\ B e. V ) <-> ( A u. B ) e. V ) $=
      ( vx vy cvv wcel wa cun cv wceq uneq1 eleq1d uneq2 visset unex vtocl2g
      wss ssun1 ssexg mpi ssun2 jca impbi ) AEFZBEFZGABHZEFZCIZDIZHZEFAUIHZEFUG
      CDABEEUHAJUJUKEUHAUIKLUIBJUKUFEUIBAMLUHUICNDNOPUGUDUEUGAUFQUDABRAUFESTUGB
      UFQUEBAUABUFESTUBUC $.
      $( [11-Jun-98] $)
  $}

  $( If the subtrahend of a class difference exists, then the minuend exists
     iff the difference exists. $)
  difex2 $p |- ( B e. C -> ( A e. V <-> ( A \ B ) e. V ) ) $=
    ( wcel cvv cdif wi difexg a1i wa cun elisset imran ancoms unexb sylib
    undif2 eleq1i wss ssun2 ssexg mpi syl exp com12 impbid ) BCDZAEDZABFZEDZUHU
    JGUGABEHIUJUGUHUJUGUHUJUGJZBAKZEDZUHUKBUIKZEDZUMUKBEDZUJJZUOUGUJUQUGUPUJBCL
    MNBUIOPUNULEBAQRPUMAULSUHABTAULEUAUBUCUDUEUF $.
    $( [12-Nov-03] $) $( [12-Nov-03] $)

  $( A triple of classes exists. $)
  tpex $p |- { A , B , C } e. V $=
    ( ctp cpr csn cun cvv df-tp prex snex unex eqeltr ) ABCDABEZCFZGHABCINOABJC
    KLM $.
    $( [10-Apr-94] $)

  $( Each member of an ordered pair belongs to the union of the union of a
     class to which the ordered pair belongs.  Lemma 3D of [Enderton] p. 41. $)
  opeluu $p |- ( <. x , y >. e. A ->
               ( x e. U. U. A /\ y e. U. U. A ) ) $=
    ( cv cop wcel cuni cpr opi2 elunii mpan visset pri1 syl pri2 jca ) ADZBDZEZ
    CFZQCGZGZFZRUBFZTQRHZUAFZUCUESFTUFQRIUESCJKZQUEFUFUCQRALMQUEUAJKNTUFUDUGRUE
    FUFUDQRBLORUEUAJKNP $.
    $( [31-Mar-95] $)

  ${
    $d x y z $.  $d y z ph $.  $( ` y z ` are dummy. $)
    $( If ` ph ` is true for exactly one ` x ` , then ` U. { x | ph } ` is a
       way to express 'the unique element such that'.   Some books use a
       special symbol such as iota to denote 'the unique element such that'. $)
    euuni $p |- ( E! x ph -> ( ph <-> U. { x | ph } = x ) ) $=
      ( vy vz weu cab cuni cv wceq wa wex wb cvv wcel euabex uniexg syl eueq
      cleqcom bieu hbab1 hbuni wel ax-17 hbeq cleq2 cbveu 3bitr sylib csn eusn
      visset snid eleq2 mpbiri abid unieq unisn syl6eq jca 19.22i sylbi
      eupickb 3exp imp3a mpd ) ABEZABFZGZBHZIZBEZAVKJZBKZJAVKLZVGVLVNVGVIMNZVLV
      GVHMNVPABOVHMPQVPCHZVIIZCEVIVQIZCEVLCVIRVRVSCVQVISTVSVKCBBDDVIVQBDVHABDUA
      UBDCUCBUDUEVKCUDVQVJVIUFUGUHUIVGVHVJUJZIZBKVNABUKWAVMBWAAVKWAVJVHNZAWAWBV
      JVTNVJBULZUMVHVTVJUNUOABUPUIWAVIVTGVJVHVTUQVJWCURUSUTVAVBUTVGVLVNVOVGVLVN
      VOAVKBVCVDVEVF $.
      $( [22-Feb-04] $) $( [22-Feb-04] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( A way to express 'the unique element such that' (restricted
       quantifier version). $)
    reuuni $p |- ( ( x e. A /\ E! x e. A ph ) ->
            ( ph <-> U. { x e. A | ph } = x ) ) $=
      ( cv wcel wa weu crab cuni wceq wb wreu cab wi euuni biimpd exp3a com12
      imp pm3.27 syl6bir adantl impbid df-rab unieqi cleq1i syl6bbr df-reu
      sylan5b ) BDZCEZUKAFZBGZAABCHZIZUJJZKABCLUKUMFZAULBMZIZUJJZUPUQAUTUKUMAUT
      NZUMUKVAUMUKAUTUMULUTULBOZPQRSUMUTANUKUMUTULAVBUKATUAUBUCUOUSUJUNURABCUDU
      EUFUGABCUHUI $.
      $( [3-Mar-04] $) $( [25-Nov-94] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reuunis.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A way to express 'the unique element such that' (restricted
       quantifier version with implicit substitution). $)
    reuunis $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( vy wcel wreu crab cuni wceq wb cv wa wi ax-17 hbreu1 hban hbrab1 hbuni
      hbeq hbbi hbim eleq1 birand cleq2 bi2bid bi2imd reuuni vtoclgf anabsi5 )
      EDHZACDIZBACDJZKZELZMZCNZDHZUNOZAUPUSLZMZPUMUNOZURPCGEDGNEHCQZVDURCUMUNCU
      MCQACDRSBUQCBCQCGGUPECGUOACGDTUAVEUBUCUDUSELZVAVDVCURVFUTUMUNUSEDUEUFVFAB
      VBUQFUSEUPUGUHUIACDUJUKUL $.
      $( [3-Mar-04] $) $( [28-Mar-97] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    $( A restricted abstraction of a wff with restricted existential
       uniqueness exists belongs to the restriction.  Useful for proving
       closure laws for definitions in theories about restricted classes. $)
    reucl $p |- ( E! x e. A ph -> U. { x e. A | ph } e. A ) $=
      ( vy cv wcel wa weu cab cuni wreu crab csn wceq wex eusn hbab1 hbuni
      ax-17 hbel visset snid eleq2 mpbiri abid sylib pm3.26d unieq unisn
      syl6req eleq1d mpbid 19.23ai sylbi df-reu df-rab unieqi eleq1i 3imtr4 )
      BEZCFZAGZBHZVBBIZJZCFZABCKABCLZJZCFVCVDUTMZNZBOVFVBBPVJVFBBDDVECBDVDVBBDQ
      RDECFBSTVJVAVFVJVAAVJUTVDFZVBVJVKUTVIFUTBUAZUBVDVIUTUCUDVBBUEUFUGVJUTVECV
      JVEVIJUTVDVIUHUTVLUIUJUKULUMUNABCUOVHVECVGVDABCUPUQURUS $.
      $( [22-Nov-94] $)
  $}

  ${
    $d A x y $.  $( ` x y ` are dummy. $)
    $( A class equals the union of its power class.  Exercise 6(a) of [Enderton]
       p. 38. $)
    unipw $p |- U. P~ A = A $=
      ( vx vy cpw cuni cv wcel wel wa wex eluni wss wi visset elpw ssel sylbi
      com12 imp 19.23aiv ssriv csn snid snex wceq eleq2 eleq1 bi2and cla4ev
      mpan snelpw 3imtr4 eqssi ) ADZEZABUOABFZUOGZBCHZCFZUNGZIZCJZUPAGZCUPUNKZV
      AVCCURUTVCUTURVCUTUSALURVCMUSACNOUSAUPPQRSTQUABAUOUPUBZUNGZVBVCUQUPVEGZVF
      VBUPBNZUCVAVGVFICVEUPUDUSVEUEURVGUTVFUSVEUPUFUSVEUNUGUHUIUJUPAVHUKVDULUAU
      M $.
      $( [14-Oct-96] $)
  $}

  ${
    $d A x $.
    $( A class is a subclass of the power class of its union.  Exercise 6(b)
       of [Enderton] p. 38. $)
    pwuni $p |- A (_ P~ U. A $=
      ( vx cuni cpw cv wcel wss elssuni visset elpw sylibr ssriv ) BAACZDZBEZAF
      OMGONFOAHOMBIJKL $.
      $( [14-Oct-96] $)
  $}

  $( The Axiom of Union and its converse.  A class is a set iff its union is a
     set. $)
  uniexb $p |- ( A e. V <-> U. A e. V ) $=
    ( cvv wcel cuni uniexg cpw wss pwuni wi pwexg ssexg syl mpi impbi ) ABCZADZ
    BCZABEQAPFZGZOAHQRBCSOIPBJARBKLMN $.
    $( [17-Nov-03] $) $( [11-Nov-03] $)

  $( The Axiom of Power Sets and its converse.  A class is a set iff its power
     class is a set. $)
  pwexb $p |- ( A e. V <-> P~ A e. V ) $=
    ( cpw cvv wcel cuni uniexb unipw eleq1i bitr2 ) ABZCDJEZCDACDJFKACAGHI $.
    $( [17-Nov-03] $) $( [11-Nov-03] $)

  $( The union of the universe is the universe.  Exercise 4.12(c) of
     [Mendelson] p. 235. $)
  univ $p |- U. V = V $=
    ( cvv cpw cuni pwv unieqi unipw eqtr3 ) ABZCACAHADEAFG $.
    $( [14-Sep-03] $)

