$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The union of a class and the Axiom of Union
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class union symbol. $)
  $c U. $. $( Big cup $)

  $( Extend class notation to include the union of a class (read:  'union A')
$)
  cuni $a class U. A $.

  ${
    $d x y A $.
    $( Define the union of a class.  Definition 5.5, p. 16, of Takeuti and
       Zaring. $)
    df-uni $a |- U. A = { x | E. y ( x e. y /\ y e. A ) } $.
  $}

  ${
    $d x A y $.  $d x B y $.  $( ` y ` is dummy. $)
    $( Membership in class union. $)
    eluni $p |- ( A e. U. B <-> E. x ( A e. x /\ x e. B ) ) $=
      ( vy cvv wcel cuni cv wa wex wb wel wceq cleq3 birand biexdv df-uni
      vtoclabg elisset adantr 19.23aiv pm5.21ni pm2.61i ) BEFZBCGZFZBAHZFZUGCFZ
      IZAJZKDALZUIIZAJUKDBUEEDHZBMZUMUJAUOULUHUIUNBUGNOPDACQRUFUDUKBUESUJUDAUHU
      DUIBUGSTUAUBUC $.
      $([22-May-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Membership in class union. $)
    elunii $p |- ( ( A e. B /\ B e. C ) -> A e. U. C ) $=
      ( vx wcel wa cv wex cuni wceq cleq4 cleq3 bi2and cla4egv anabsi7 eluni
      sylibr ) ABEZBCEZFZADGZEZUACEZFZDHZACIERSUEUDTDBCUABJUBRUCSUABAKUABCLMNOD
      ACPQ $.
      $([24-Mar-95]$)
  $}

  ${
    $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbuni.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound variable hypothesis builder for union. $)
    hbuni   $p |- ( y e. U. A -> A. x y e. U. A ) $=
      ( vz wel cv wcel wa wex wal cuni ax-17 hbel hban hbex eluni bial 3imtr4
      ) BEFZEGZCHZIZEJZUDAKBGZCLHZUFAKUCAETUBATAMZABBUACUGDNOPEUECQZUFUDAUHRS
      $.
      $([30-Dec-96]$)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( Membership in union of a class abstract. $)
    eluniab   $p |- ( A e. U. { x | ph } <-> E. x ( A e. x /\ ph ) ) $=
      ( vy cab cuni wcel cv wa wex eluni ax-17 hbab1 hban weq cleq4 cleq3
      bi2and abid bilan syl6bbr cbvex bitr ) CABEZFGCDHZGZUEUDGZIZDJCBHZGZAIZB
      JDCUDKUHUKDBUFUGBUFBLABDMNUKDLDBOZUHUJUIUDGZIUKULUFUJUGUMUEUICPUEUIUDQRAU
      MUJABSTUAUBUC $.
      $([11-Aug-94]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Equality theorem for class union.  Exercise 15 of Takeuti and Zaring,
       p. 18. $)
    unieq $p |- ( A = B -> U. A = U. B ) $=
      ( vx vy wceq wel cv wcel wa wex cab cuni cleq4 biland biexdv biabdv
      df-uni 3eqtr4g ) ABEZCDFZDGZAHZIZDJZCKTUABHZIZDJZCKALBLSUDUGCSUCUFDSUBUET
      ABUAMNOPCDAQCDBQR $.
      $([10-Aug-93]$)
  $}

  ${
    unieqi.1 $e |- A = B $.
    $( Inference of equality of two class unions. $)
    unieqi   $p |- U. A = U. B $=
      ( wceq cuni unieq ax-mp ) ABDAEBEDABFCG $.
      $([30-Aug-93]$)
  $}

  ${
    unieqd.1 $e |- ( ph -> A = B ) $.
    $( Deduction of equality of two class unions. $)
    unieqd   $p |- ( ph -> U. A = U. B ) $=
      ( wceq cuni unieq syl ) ABCEBFCFEDBCGH $.
      $([21-Apr-95]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The union of the empty set is the empty set.  Theorem 8.7 of Quine,
       p. 54. $)
    uni0 $p |- (/) = U. (/) $=
      ( vx vy weq wn cab wel cv c0 wcel wa wex cuni noel intnan nex eqid 2th
      bicon1 biab dfnul2 df-uni 3eqtr4 ) AACZDZAEABFZBGZHIZJZBKZAEHHLUDUIAUIUCU
      IDUCUHBUGUEUFMNOAPQRSATABHUAUB $.
      $([16-Sep-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    unpr.1 $e |- A e. V $.
    unpr.2 $e |- B e. V $.
    $( The union of two sets is the union of their pair.  Proposition 5.7,
       p. 16, of Takeuti and Zaring. $)
    unpr   $p |- ( A u. B ) = U. { A , B } $=
      ( vx vy cv wcel wo cab wel cpr wa wex cun cuni wceq 19.43 visset pri
      bilan andi bitr biex clel3 exancom bi2or 3bitr4r biab dfun2 df-uni
      3eqtr4 ) EGZAHZUMBHZIZEJEFKZFGZABLZHZMZFNZEJABOUSPUPVBEUQURAQZMZUQURBQZMZ
      IZFNVDFNZVFFNZIVBUPVDVFFRVAVGFVAUQVCVEIZMVGUTVJUQURABFSTUAUQVCVEUBUCUDUNV
      HUOVIUNVCUQMFNVHFUMACUEVCUQFUFUCUOVEUQMFNVIFUMBDUEVEUQFUFUCUGUHUIEABUJEFU
      SUKUL $.
      $([23-Aug-93]$)
  $}

  ${
    unisn.1 $e |- A e. V $.
    $( A set equals the union of its singleton.  Theorem 8.2 of Quine,
       p. 53. $)
    unisn   $p |- A = U. { A } $=
      ( cun cpr cuni csn unpr unabs dfsn2 unieqi 3eqtr4 ) AACAADZEAAFZEAABBGAHM
      LAIJK $.
      $([30-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Distribution of class union over union of classes.  Theorem 8.3 of
       Quine, p. 53. $)
    undist $p |- U. ( A u. B ) = ( U. A u. U. B ) $=
      ( vx vy cun cuni wel cv wcel wo wa wex andi biex eluni elun bilan bitr
      bi2or 19.43 3bitr4 cleqri ) CABEZFZAFZBFZEZCDGZDHZAIZUIBIZJZKZDLZUHUJKZUH
      UKKZJZDLZCHZUDIZUSUGIZUMUQDUHUJUKMNUTUHUIUCIZKZDLUNDUSUCOVCUMDVBULUHUIABP
      QNRUSUEIZUSUFIZJUODLZUPDLZJVAURVDVFVEVGDUSAODUSBOSUSUEUFPUOUPDTUAUAUB $.
      $([20-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Subset relationship for class union. $)
    ssuni $p |- ( ( A (_ B /\ B e. C ) -> A (_ U. C ) $=
      ( vx vy wss wcel cuni cv wi wceq sseq2 birimd wel wal wa wex 19.8a exp
      comm eluni syl6ibr syl3d 19.21aiiv df-ss 3imtr4g vtoclga imp ) ABFZBCGZA
      CHZFZUJUIULADIZFZULJUIULJDBCUMBKUNUIULUMBALMUMCGZEIZAGZEDNZJZEOUQUPUKGZJZ
      EOUNULUOUSVAEUOURUTUQUOURURUOPZDQZUTURUOVCURUOVCVBDRSTDUPCUAUBUCUDEAUMUEE
      AUKUEUFUGTUH $.
      $([24-May-94]$)
  $}

  $( An element of a class is a subset of its union.  Theorem 8.6 of Quine,
     p. 54. $)
  elssuni $p |- ( A e. B -> A (_ U. B ) $=
      ( wss wcel cuni ssuni ssid mpan ) AACABDABECAABFAGH $.
      $([6-Jun-94]$)

  ${
    $d x y A $.  $d x y B $.  $( ` y ` is dummy. $)
    $( Relationship involving membership, subset, and union.  Exercise 5
       of Enderton, p. 26. $)
    ssuniss $p |- ( A. x ( x e. A -> x (_ B ) -> U. A (_ B ) $=
      ( vy cv wcel wss wi wal cuni wel wa wex id imland ssel comm imp syl6
      19.20i ax-17 19.23 sylib eluni syl5ib ssrdv ) AEZBFZUGCGZHZAIZDBJZCUKDAK
      ZUHLZAMZDEZCFZUPULFUKUNUQHZAIUOUQHUJURAUJUNUMUILUQUJUHUIUMUJNOUMUIUQUIUMU
      QUGCUPPQRSTUNUQAUQAUAUBUCAUPBUDUEUF $.
      $([19-Mar-96]$)
  $}

  ${
    $d x y z w A $.  $( ` x y z w ` are dummy. $)
    zfun2.1 $e |- A e. V $.
    $( The ZF Axiom of Union in class notation.   Same as Axiom 3, p. 16, of
       Takeuti and Zaring. $)
    zfun2   $p |- U. A e. V $=
      ( vz vx vy cv cuni cvv wcel wceq unieq cleq3d wex wel wb wal dfcleq
      biex wi wa eluni birim bial ax-un mpbir bm1.3ii issetri vtoclbv ) CFZGZHI
      AGZHICABUIAJUJUKHUIAKLDUJDFZUJJZDMEDNZEFZUJIZOEPZDMUMUQDEULUJQRUPDEUPUNSZ
      EPZDMUNDCNTDMZUNSZEPZDMUSVBDURVAEUPUTUNDUOUIUAUBUCRDECUDUEUFUEUGUH $.
      $([11-Aug-93]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The ZF Axiom of Union in class notation, generalized. $)
    zfun2g   $p |- ( A e. B -> U. A e. V ) $=
      ( vx cv cuni cvv wcel wceq unieq cleq3d visset zfun2 vtoclg ) CDZEZFGAEZ
      FGCABNAHOPFNAIJNCKLM $.
      $([25-Nov-94]$)
  $}

  ${
    unex.1 $e |- A e. V $.
    unex.2 $e |- B e. V $.
    $( The union of two sets is a set.  Corollary 5.8, p. 16, of Takeuti and
       Zaring. $)
    unex   $p |- ( A u. B ) e. V $=
      ( cun cpr cuni cvv unpr zfpaircl zfun2 eqeltr ) ABEABFZGHABCDIMABJKL $.
      $([1-Jul-94]$)
  $}

  $( A triple of classes exists. $)
  tpex $p |- { A , B , C } e. V $=
      ( ctp cpr csn cun cvv df-tp zfpaircl snex unex eqeltr ) ABCDABEZCFZGHABCI
      NOABJCKLM $.
      $([10-Apr-94]$)

  $( Each member of an ordered pair belongs to the union of the union of a
     class to which the ordered pair belongs.  Lemma 3D of Enderton, p. 41. $)
  opeluu $p |- ( <. x , y >. e. A ->
               ( x e. U. U. A /\ y e. U. U. A ) ) $=
      ( cv cop wcel cuni cpr elunii opi2 mpan visset pri1 syl pri2 jca ) ADZBDZ
      EZCFZQCGZGZFZRUBFZTQRHZUAFZUCUESFTUFUESCIQRJKZQUEFUFUCQUEUAIQRALMKNTUFUDU
      GRUEFUFUDRUEUAIQRBLOKNP $.
      $([31-Mar-95]$)

  ${
    $d x y z $.  $d y z ph $.  $( ` y z ` are dummy. $)
    $( A way to express 'the unique element such that'. $)
    euuni $p |- ( E! x ph -> ( ph <-> x = U. { x | ph } ) ) $=
      ( vy vz weu cv cab cuni wceq wa wex wb eupickb 3exp imp3a cvv wcel
      euabex zfun2g syl eueq ax17el wel wal hbe1 eluniab bial 3imtr4 hbeq
      ax-17 cleq1 cbveu bitr sylib csn eusn cleq4 visset snid mpbii abid
      sylibr unieq unisn syl5eq jca 19.22i sylbi mpd ) ABEZBFZABGZHZIZBEZAVNJZ
      BKZJAVNLZVJVOVQVRVJVOVQVRAVNBMNOVJVOVQVJVMPQZVOVJVLPQVSABRVLPSTVSCFZVMIZC
      EVOCVMUAWAVNCBBDDVTVMDCBUBDBUCAJZBKZWCBUDDFZVMQZWEBUDWBBUEABWDUFZWEWCBWFU
      GUHUIVNCUJVTVKVMUKULUMUNVJVKUOZVLIZBKVQABUPWHVPBWHAVNWHVKVLQZAWHVKWGQWIWG
      VLVKUQVKBURZUSUTABVAVBWHWGHVMVKWGVLVCVKWJVDVEVFVGVHVFVI $.
      $([22-Nov-94]$)
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( A way to express 'the unique element such that' (restricted
       variable version). $)
    reuuni $p |- ( ( x e. A /\ E! x e. A ph ) ->
            ( ph <-> x = U. { x e. A | ph } ) ) $=
      ( cv wcel wa weu crab cuni wceq wb wreu cab wi euuni biimpd exp3a comm
      imp pm3.27 syl6bir adantl impbid df-rab unieqi cleq2i syl6bbr df-reu
      sylan5b ) BDZCEZUKAFZBGZAUJABCHZIZJZKABCLUKUMFZAUJULBMZIZJZUPUQAUTUKUMAUT
      NZUMUKVAUMUKAUTUMULUTULBOZPQRSUMUTANUKUMUTULAVBUKATUAUBUCUOUSUJUNURABCUDU
      EUFUGABCUHUI $.
      $([25-Nov-94]$)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.  $( ` y ` is dummy. $)
    reuunis.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A way to express 'the unique element such that' (restricted
       variable version with implicit substitution). $)
    reuunis $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> B = U. { x e. A | ph } ) ) $=
      ( vy wcel wreu crab cuni wceq wb cv wa wi ax-17 hbreu1 hban hbrab1 hbuni
      hbeq hbbi hbim cleq3 birand cleq1 bi2bid bi2imd reuuni vtoclgf
      anabsi5 ) EDHZACDIZBEACDJZKZLZMZCNZDHZUNOZAUSUPLZMZPUMUNOZURPCEDVDURCUMUN
      CUMCQACDRSBUQCBCQCGGEUPGNEHCQCGUOACGDTUAUBUCUDUSELZVAVDVCURVEUTUMUNUSEDUE
      UFVEABVBUQFUSEUPUGUHUIACDUJUKUL $.
      $([28-Mar-97]$)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    $( A restricted abstraction of a wff with restricted existential
       uniqueness exists belongs to the restriction.  Useful for proving
       closure laws for definitions in theories about restricted classes. $)
    reucl $p |- ( E! x e. A ph -> U. { x e. A | ph } e. A ) $=
      ( vy cv wcel wa weu cab cuni wreu crab csn wceq wex eusn wel wal hbe1
      eluniab bial 3imtr4 ax-17 hbel unieq visset unisn syl5eq cleq3d cleq4
      snid mpbii abid sylibr pm3.26d mpbid 19.23ai sylbi df-reu df-rab
      unieqi cleq3i ) BEZCFZAGZBHZVEBIZJZCFZABCKABCLZJZCFVFVCMZVGNZBOVIVEBPVMVI
      BBDDVHCDBQVEGZBOZVOBRDEZVHFZVQBRVNBSVEBVPTZVQVOBVRUAUBVPCFBUCUDVMVDVIVMVC
      VHCVMVLJVHVCVLVGUEVCBUFZUGUHUIVMVDAVMVCVGFZVEVMVCVLFVTVLVGVCUJVCVSUKULVEB
      UMUNUOUPUQURABCUSVKVHCVJVGABCUTVAVBUB $.
      $([22-Nov-94]$)
  $}

  ${
    $d A x y $.  $( ` x y ` are dummy. $)
    $( A class equals the union of its power class.  Exercise 6(a) of Enderton,
       p. 38. $)
    unipw   $p |- U. P~ A = A $=
      ( vx vy cpw cuni cv wcel wel wa wex eluni wss wi visset sselpw ssel
      sylbir comm imp 19.23aiv sylbi ssri csn snex wceq cleq4 cleq3 bi2and
      cla4ev snid mpan snss bitr 3imtr4 eqssi ) ADZEZABUQABFZUQGZBCHZCFZUPGZIZC
      JZURAGZCURUPKZVCVECUTVBVEVBUTVEVBVAALUTVEMVAACNOVAAURPQRSTUAUBBAUQURUCZUP
      GZVDVEUSURVGGZVHVDVCVIVHICVGURUDZVAVGUEUTVIVBVHVAVGURUFVAVGUPUGUHUIURBNZU
      JUKVEVGALVHURAVKULVGAVJOUMVFUNUBUO $.
      $([14-Oct-96]$)
  $}

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( A class is a subclass of the power class of its union.  Exercise 6(b)
       of Enderton, p. 38. $)
    pwuni   $p |- A (_ P~ U. A $=
      ( vx cuni cpw cv wcel wss elssuni visset sselpw sylib ssri ) BAACZDZBEZAF
      OMGONFOAHOMBIJKL $.
      $([14-Oct-96]$)
  $}

