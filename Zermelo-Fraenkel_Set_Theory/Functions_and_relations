$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Functions and relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c X. $. $( Times symbol (cross product symbol) (read: 'cross') $)
  $c `' $. $( Small elevated smiley (converse operation) $)
  $c dom $. $( Domain $)
  $c ran $. $( Range $)
  $c |` $. $( Right hook (restriction symbol) $)
  $c " $. $( Left quote (image symbol) $)
  $c o. $. $( Small circle (composition symbol) $)
  $c Rel $. $( Relation predicate $)
  $c Fun $. $( Function predicate $)
  $c Fn $. $( Function connective $)
  $c : $. $( Colon $)
  $c --> $. $( Domain-range connective $)
  $c -1-1-> $. $( 'One-to-one' domain-range connective $)
  $c -onto-> $. $( 'Onto' domain-range connective $)
  $c -1-1-onto-> $. $( 'One-to-one' and 'onto' domain-range connective $)
  $c ` $. $( Left apostrophe (function value symbol) $)
  $c Isom $. $( Isomorphism $)

  $( Introduce new variables.  These are no different from ordinary variables,
     but their names serve as mnemonics to remind us that they usually
     represent functions. $)
  $v f $.
  $v g $.
  $v h $.
  $v F $.
  $v G $.
  $v H $.

  $( Let ` f ` be an individual variable. $)
  vf $f set f $.
  $( Let ` g ` be an individual variable. $)
  vg $f set g $.
  $( Let ` h ` be an individual variable. $)
  vh $f set h $.
  $( Let ` F ` be a class variable. $)
  cF $f class F $.
  $( Let ` G ` be a class variable. $)
  cG $f class G $.
  $( Let ` H ` be a class variable. $)
  cH $f class H $.

  $( Extend the definition of a class to include the cross product. $)
  cxp $a class ( A X. B ) $.

  $( Extend the definition of a class to include the converse of a class. $)
  ccnv $a class `' A $.

  $( Extend the definition of a class to include the domain of a class. $)
  cdm $a class dom A $.

  $( Extend the definition of a class to include the range of a class. $)
  crn $a class ran A $.

  $( Extend the definition of a class to include the restriction of a class.
     (Read:  The restriction of ` A ` to ` B ` .) $)
  cres $a class ( A |` B ) $.

  $( Extend the definition of a class to include the image of a class.
     (Read:  The image of ` B ` under ` A ` .) $)
  cima $a class ( A " B ) $.

  $( Extend the definition of a class to include the composition of two
     classes.  (Read:  The composition of ` A ` and ` B ` .) $)
  ccom $a class ( A o. B ) $.

  $( Extend the definition of a wff to include the relation predicate.  (Read:
     ` A ` is a relation.) $)
  wrel $a wff Rel A $.

  $( Extend the definition of a wff to include the function predicate.  (Read:
     ` A ` is a function.) $)
  wfun $a wff Fun A $.

  $( Extend the definition of a wff to include the function predicate with
     a domain.  (Read:  ` A ` is a function on ` B ` .) $)
  wfn $a wff A Fn B $.

  $( Extend the definition of a wff to include the function predicate with
     domain and range.  (Read:  ` F ` maps ` A ` into ` B ` .) $)
  wf $a wff F : A --> B $.

  $( Extend the definition of a wff to include one-to-one functions.  (Read:
     ` F ` maps ` A ` one-to-one into ` B ` .) $)
  wf1 $a wff F : A -1-1-> B $.

  $( Extend the definition of a wff to include onto functions.  (Read:  ` F `
     maps ` A ` onto ` B ` .) $)
  wfo $a wff F : A -onto-> B $.

  $( Extend the definition of a wff to include one-to-one onto functions.
      (Read:  ` F ` maps ` A ` one-to-one onto ` B ` .) $)
  wf1o $a wff F : A -1-1-onto-> B $.

  $( Extend the definition of a class to include the value of a function.
     (Read:  The value of ` F ` at ` A ` , or " ` F ` of ` A ` ".) $)
  cfv $a class ( F ` A ) $.

  $( Extend the definition of a wff to include the isomorphism property.
     (Read:  ` H ` is an ` R ` , ` S ` isomorphism of ` A ` onto ` B ` .) $)
  wiso $a wff H Isom R , S ( A , B ) $.

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.

    $( Define the cross product of two classes.  Definition 9.11 of [Quine]
       p. 64. $)
    df-xp $a |- ( A X. B ) = { <. x , y >. | ( x e. A /\ y e. B ) } $.

    $( Define a relation.  Definition 6.4(1) of [TakeutiZaring] p. 23.
       For an alternate definition, see ~ dfrel2 . $)
    df-rel $a |- ( Rel A <-> A (_ ( V X. V ) ) $.

    $( Define the converse of a class.  Definition 9.12 of [Quine] p. 64.
       We use Quine's breve accent (smile) notation; as a prefix, it
       eliminates parentheses for us.  Many authors use the postfix superscript
       "to the minus one".  $)
    df-cnv $a |- `' A = { <. x , y >. | y A x } $.

    $( Define the composition of two classes.  Definition 6.6(3) of
       [TakeutiZaring] p. 24.  Note that Definition 7 of [Suppes] p. 63
       reverses ` A ` and ` B ` , uses ` /. ` instead of ` o. ` , and
       calls the operation "relative product." $)
    df-co $a |- ( A o. B ) = { <. x , y >. | E. z ( x B z /\ z A y ) } $.

    $( Define the domain of a class.  Definition 3 of [Suppes] p. 59. $)
    df-dm $a |- dom A = { x | E. y x A y } $.

    $( Define the range of a class.  For an alternate definition, see
       ~ dfrn2 . $)
    df-rn $a |- ran A = dom `' A $.

    $( Define the restriction of a class.  Definition 6.6(1) of [TakeutiZaring]
       p. 24. $)
    df-res $a |- ( A |` B ) = ( A i^i ( B X. V ) ) $.

    $( Define the image of a class.  Definition 6.6(2) of [TakeutiZaring]
       p. 24.  For an alternate definition, see ~ dfima2 . $)
    df-ima $a |- ( A " B ) = ran ( A |` B ) $.

    $( Define a function.  Definition 10.1 of [Quine] p. 65.  For alternate
       definitions, see ~ dffun2 , ~ dffun3 , ~ dffun4 , ~ dffun5 , ~ dffunmo ,
       ~ dffun6 , and ~ dffun7 . $)
    df-fun $a |- ( Fun A <-> ( Rel A /\ ( A o. `' A ) (_ I ) ) $.

    $( Define a function with domain.  Definition 6.15(1) of
       [TakeutiZaring] p. 27. $)
    df-fn $a |- ( A Fn B <-> ( Fun A /\ dom A = B ) ) $.

    $( Define a function (mapping) with domain and co-domain.  Definition
       6.15(3) of [TakeutiZaring] p. 27. $)
    df-f $a |- ( F : A --> B <-> ( F Fn A /\ ran F (_ B ) ) $.

    $( Define a one-to-one function.  For an equivalent definition see ~ f11 .
       Compare Definition 6.15(5) of [TakeutiZaring] p. 27. $)
    df-f1 $a |- ( F : A -1-1-> B <-> ( F : A --> B /\ Fun `' F ) ) $.

    $( Define an onto function.  Definition 6.15(4) of [TakeutiZaring]
       p. 27. $)
    df-fo $a |- ( F : A -onto-> B <-> ( F Fn A /\ ran F = B ) ) $.

    $( Define a one-to-one onto function.  For equivalent definitions see
       ~ f1o2 , ~ f1o3 , ~ f1o4 , and ~ f1o5 .  Compare Definition 6.15(6) of
      [TakeutiZaring] p. 27. $)
    df-f1o $a |- ( F : A -1-1-onto-> B <->
                ( F : A -1-1-> B /\ F : A -onto-> B ) ) $.

    $( Define the value of a function.  Although it has roots in Definition
       10.2 of [Quine] p. 65, our definition apparently does not appear in the
       literature but is quite convenient:  it can be applied to any class
       and evaluates to the empty set when it is not meaningful.  The left
       apostrophe notation is common in set theory and means the same thing as
       the more familiar ` F ( A ) ` notation for a function's value at ` A ` ,
       i.e. " ` F ` of ` A ` ", but without context-dependent ambiguity.  For
       more conventional alternate definitions, see ~ fv2 and ~ fv3 ;
       restricted equivalents are shown in ~ funfv and ~ funfv2 .  For the
       familiar definition of function value in terms of ordered pair
       membership see ~ funfvop . $)
    df-fv $a |- ( F ` A ) = U. { x | ( F " { A } ) = { x } } $.
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $( Define the isomorphism predicate.  We read this as " ` H ` is an ` R ` ,
       ` S ` isomorphism of ` A ` onto ` B ` ."  Normally, ` R ` and ` S `
       are ordering relations on ` A ` and ` B ` respectively.  Definition 6.28
       of [TakeutiZaring] p. 32, whose notation is the same as ours except
       that ` R ` and ` S ` are subscripts. $)
    df-iso $a |- ( H Isom R , S ( A , B ) <-> ( H : A -1-1-onto-> B /\
                 A. x e. A A. y e. A ( x R y <-> ( H ` x ) S ( H ` y ) ) ) ) $.

  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
      $( ` x y z ` are dummy. $)
    $( Equality theorem for cross product. $)
    xpeq1 $p |- ( A = B -> ( A X. C ) = ( B X. C ) ) $=
      ( vx vy wceq cv wcel wa copab cxp eleq2 birand biopabdv df-xp 3eqtr4g )
      ABFZDGZAHZEGCHZIZDEJRBHZTIZDEJACKBCKQUAUCDEQSUBTABRLMNDEACODEBCOP $.
      $( [4-Jul-94] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
      $( ` x y z ` are dummy. $)
    $( Equality theorem for cross product. $)
    xpeq2 $p |- ( A = B -> ( C X. A ) = ( C X. B ) ) $=
      ( vx vy wceq cv wcel wa copab cxp eleq2 biland biopabdv df-xp 3eqtr4g )
      ABFZDGCHZEGZAHZIZDEJRSBHZIZDEJCAKCBKQUAUCDEQTUBRABSLMNDECAODECBOP $.
      $( [5-Jul-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in a cross product. $)
    elxp $p |- ( A e. ( B X. C ) <-> E. x E. y ( A = <. x , y >. /\
               ( x e. B /\ y e. C ) ) ) $=
      ( cxp wcel cv wa copab cop wceq wex df-xp eleq2i elopab bitr ) CDEFZGCAHZ
      DGBHZEGIZABJZGCSTKLUAIBMAMRUBCABDENOUAABCPQ $.
      $( [4-Jul-94] $)

    $( Membership in a cross product. $)
    elxp2 $p |- ( A e. ( B X. C ) <-> E. x e. B E. y e. C A = <. x , y >.  ) $=
      ( cv wcel cop wceq wrex wa wex cxp df-rex r19.42v anass ancom an12
      3bitr4r biex 3bitr3 elxp ) AFZDGZCUCBFZHIZBEJZKZALUFUDUEEGZKZKZBLZALUGADJ
      CDEMGUHULAUDUFKZBEJUIUMKZBLUHULUMBENUDUFBEOUNUKBUJUFKUDUIUFKKUKUNUDUIUFPU
      FUJQUIUDUFRSTUATUGADNABCDEUBS $.
      $( [23-Feb-04] $) $( [23-Feb-04] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d x y z w $.
    hbxp.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbxp.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for cross product. $)
    hbxp $p |- ( y e. ( A X. B ) -> A. x y e. ( A X. B ) ) $=
      ( vz vw cv cop wceq wcel wa wex wal cxp ax-17 wel hbel hban hbex elxp
      bial 3imtr4 ) BIZGIZHIZJKZUFCLZUGDLZMZMZHNZGNZUNAOUECDPLZUOAOUMAGULAHUHUK
      AUHAQUIUJAABBUFCBGRAQESABBUGDBHRAQFSTTUAUAGHUECDUBZUOUNAUPUCUD $.
      $( [15-Sep-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $( ` x y ` are dummy. $)
    $( The first member of an ordered pair of classes in a cross product
       exists.  (This is a byproduct of our definition of ordered pair.
       Unfortunately existence is not implied for the second member.) $)
    opelxpex $p |- ( <. A , B >. e. ( C X. D ) -> A e. V ) $=
      ( vx vy cop cxp wcel cv wceq wa wex cvv elxp visset c0 wn eleq2 opprc1b
      3bitr4g bicon4d mpbiri adantr 19.23aivv sylbi ) ABGZCDHIUGEJZFJZGZKZUHCIU
      IDILZLZFMEMANIZEFUGCDOUMUNEFUKUNULUKUNUHNIZEPUKUNUOUKQUGIQUJIUNRUORUGUJQS
      ABTUHUITUAUBUCUDUEUF $.
      $( [8-Nov-95] $)
  $}

  $( A true binary relation on a relation implies the first argument is a set.
     (This is a property of our ordered pair definition.) $)
  brrelex $p |- ( ( Rel R /\ A R B ) -> A e. V ) $=
    ( wrel wbr cvv wcel cxp wss wi df-rel cop ssel df-br syl5ib opelxpex syl6
    sylbi imp ) CDZABCEZAFGZTCFFHZIZUAUBJCKUDUAABLZUCGZUBUDUECGUFUACUCUEMABCNOA
    BFFPQRS $.
    $( [18-May-04] $) $( [18-May-04] $)

  ${
    brrelexi.1 $e |- Rel R $.
    $( The first argument of a binary relation exists.  (An artifact of our
       ordered pair definition.) $)
    brrelexi $p |- ( A R B -> A e. V ) $=
      ( wrel wbr cvv wcel brrelex mpan ) CEABCFAGHDABCIJ $.
      $( [4-Jun-98] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Representation of a constant function using ordered pairs. $)
    fconstopab $p |- ( A X. { B } ) = { <. x , y >. | ( x e. A /\ y = B ) } $=
      ( csn cxp cv wcel wa copab wceq df-xp elsn bilan biopab eqtr ) CDEZFAGCHZ
      BGZQHZIZABJRSDKZIZABJABCQLUAUCABTUBRBDMNOP $.
      $( [12-Oct-99] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
        $( ` x y z ` are dummy. $)
    vtoclr.1 $e |- Rel R $.
    vtoclr.2 $e |- ( ( x R y /\ y R z ) -> x R z ) $.
    $( Variable to class conversion of transitive relation. $)
    vtoclr $p |- ( C e. D -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( wcel cvv wbr wa wi elisset cv wceq breq1 birand bi2imd bilimd breq2
      bi2and birimd biland vtoclg vtocl2g brrelexi syl2an pm2.43b syl ) FGKFLKZ
      DEHMZEFHMZNZDFHMZOZFGPUMUPUQDLKELKUMUROZUNUOUMAQZBQZHMZVAFHMZNZUTFHMZOZOU
      MDVAHMZVCNZUQOZOUSABDELLUTDRZVFVIUMVJVDVHVEUQVJVBVGVCUTDVAHSTUTDFHSUAUBVA
      ERZVIURUMVKVHUPUQVKVGUNVCUOVAEDHUCVAEFHSUDUEUBVBVACQZHMZNZUTVLHMZOVFCFLVL
      FRZVNVDVOVEVPVMVCVBVLFVAHUCUFVLFUTHUCUAJUGUHDEHIUIEFHIUIUJUKUL $.
      $( [9-Jun-98] $)

    ${
      vtoclrbr.3 $e |- x R x $.
      $( Variable to class conversion of transitive, reflexive relation. $)
      vtoclrbr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( cvv wcel wbr wa wi vtoclr wn brprc cv wceq breq1 breq2 bitrd vtoclg
        syl5bir brrelexi syl5 adantrd pm2.61i ) FKLZDEGMZEFGMZNDFGMZOABCDEFKGHI
        PUJQZUKUMULUNDKLZUMUKUNUMDDGMZUODFGRASZUQGMZUPADKUQDTURDUQGMUPUQDUQGUAU
        QDDGUBUCJUDUEDEGHUFUGUHUI $.
        $( [9-Jun-98] $)
    $}

    ${
      vtoclibr.3 $e |- -. x R x $.
      $( Variable to class conversion of transitive, irreflexive relation. $)
      vtoclibr $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( wbr cvv wcel wa wi wn cv wceq breq1 breq2 bitrd binegd vtoclg
        brrelexi con3i pm2.61i brprc mtbiri a3i vtoclr syl anabsi7 ) DEGKZEFGKZ
        DFGKZUNFLMZUMUNNUOOUPUNUPPUNEEGKZELMZUQPZAQZUTGKZPUSAELUTERZVAUQVBVAEUT
        GKUQUTEUTGSUTEEGTUAUBJUCUQUREEGHUDUEUFEFGUGUHUIABCDEFLGHIUJUKUL $.
        $( [9-Jun-98] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
      $( ` x y z ` are dummy. $)
    opelxp.1 $e |- B e. V $.
    $( Ordered pair membership in a cross product. $)
    opelxp $p |- ( <. A , B >. e. ( C X. D ) <-> ( A e. C /\ B e. D ) ) $=
      ( vz vx vy cop cxp wcel cvv wa opelxpex elisset adantr cv wceq opeq1
      eleq1d eleq1 birand wex weq cleqcom visset opth bitr biran an4 biex
      19.42v 19.41v elxp df-clel bi2an 3bitr4 vtoclbg pm5.21nii ) ABIZCDJZKZALK
      ZACKZBDKZMZABCDNVDVCVEACOPFQZBIZVAKZVGCKZVEMZVBVFFALVGARZVHUTVAVGABSTVLVJ
      VDVEVGACUAUBVHGQZHQZIZRZVMCKZVNDKZMZMZHUCZGUCZGFUDZVQMZGUCZVNBRZVRMZHUCZM
      ZVIVKWBWDWHMZGUCWIWAWJGWAWDWGMZHUCWJVTWKHVTWCWFMZVSMWKVPWLVSVPVOVHRWLVHVO
      UEVMVNVGBGUFHUFEUGUHUIWCWFVQVRUJUHUKWDWGHULUHUKWDWHGUMUHGHVHCDUNVJWEVEWHG
      VGCUOHBDUOUPUQURUS $.
      $( [15-Nov-94] $)

    $( Binary relation on a cross product. $)
    brxp $p |- ( A ( C X. D ) B <-> ( A e. C /\ B e. D ) ) $=
      ( cxp wbr cop wcel wa df-br opelxp bitr ) ABCDFZGABHNIACIBDIJABNKABCDELM
      $.
      $( [14-May-04] $) $( [22-Apr-04] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.  $( ` x ` is dummy. $)
    $( Ordered pair membership in a cross product. $)
    opelxpg $p |- ( B e. R ->
                    ( <. A , B >. e. ( C X. D ) <-> ( A e. C /\ B e. D ) ) ) $=
      ( vx cv cop cxp wcel wa wceq opeq2 eleq1d eleq1 biland visset opelxp
      vtoclbg ) AFGZHZCDIZJACJZTDJZKABHZUBJUCBDJZKFBETBLZUAUEUBTBAMNUGUDUFUCTBD
      OPATCDFQRS $.
      $( [28-May-95] $)
  $}

  $( Ordered pair membership in a cross product (implication). $)
    opelxpi $p |- ( ( A e. C /\ B e. D ) -> <. A , B >. e. ( C X. D ) ) $=
      ( wcel cop cxp wa opelxpg biimprd anabsi7 ) ACEZBDEZABFCDGEZMNLMHABCDDIJK
      $.
      $( [28-May-95] $)

  ${
    $d x y z A $.  $d x y z B $.  $d y z ph $.  $d x ps $.
    ralxp.1 $e |- ( x = <. y , z >. -> ( ph <-> ps ) ) $.
    $( Universal quantification restricted to a cross product is equivalent
       to a double restricted quantification.  The hypothesis specifies an
       implicit substitution. $)
    ralxp $p |- ( A. x e. ( A X. B ) ph <-> A. y e. A A. z e. B ps ) $=
      ( cxp wral cv wcel cop wa rcla4v visset opelxp syl5ibr exp3a r19.21adv
      r19.21aiv wceq wex wi elxp pm3.26 19.22i sylbi hbra1 ax-17 hbim hbral
      eleq1 syl6bb bi2imd ra42 syl5bir 19.23ai syl pm2.43b impbi ) ACFGIZJZBEGJ
      ZDFJZVCVDDFVCDKZFLZBEGVCVGEKZGLZBVCVFVHMZVBLZBVGVINZABCVJVBHOVFVHFGEPQZRS
      TUAVEACVBVECKZVBLZAVOVNVJUBZEUCZDUCZVEVOAUDZUDZVOVPVLNZEUCZDUCVRDEVNFGUEW
      BVQDWAVPEVPVLUFUGUGUHVQVTDVEVSDVDDFUIVSDUJUKVPVTEVEVSEVDEDFVGEUJBEGUIULVS
      EUJUKVPVSVLBUDVEVPVOVLABVPVOVKVLVNVJVBUMVMUNHUOBDEFGUPUQURURUSUTUAVA $.
      $( [4-Mar-04] $) $( [7-Feb-04] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( Justification theorem for an ordered pair definition that works for
       any classes, including proper classes.  This is the definition implied
       by the footnote in [Jech] p. 78, which says, "The sophisticated reader
       will not object to our use of a pair of classes." $)
    opthprc $p |- ( ( ( A X. { (/) } ) u. ( B X. { { (/) } } ) ) =
                    ( ( C X. { (/) } ) u. ( D X. { { (/) } } ) )
                  <-> ( A = C /\ B = D ) ) $=
      ( vx c0 csn cxp cun wceq wa cv cop wcel eleq2 wo 0ex opelxp snid
      mpbiranr 0nep0 elsnc mtbir bianfi bitr4 bi2or elun biorfi 3bitr4r
      3bitr4g cleqrd p0ex cleqcom bitr wn wb biorf ax-mp jca uneq12 xpeq1
      syl2an impbi ) AFGZHZBVDGZHZIZCVDHZDVFHZIZJZACJZBDJZKVLVMVNVLEACVLELZFMZV
      HNZVPVKNZVOANZVOCNZVHVKVPOVPVENZVPVGNZPVSFVFNZPVQVSWAVSWBWCWAVSFVDNZVOFAV
      DQRFQSZTWBVOBNZWCKWCVOFBVFQRWCWFWCFVDJZUAFVDQUBUCZUDUEUFVPVEVGUGWCVSWHUHU
      IVPVINZVPVJNZPVTWCPVRVTWIVTWJWCWIVTWDVOFCVDQRWETWJVODNZWCKWCVOFDVFQRWCWKW
      HUDUEUFVPVIVJUGWCVTWHUHUIUJUKVLEBDVLVOVDMZVHNZWLVKNZWFWKVHVKWLOWLVENZWLVG
      NZPVDVDNZWFPZWMWFWOWQWPWFWOVSWQKWQVOVDAVDULRWQVSWQWGUAWQVDFJWGVDFULUBVDFU
      MUNUCZUDUEWPWFVDVFNZVOVDBVFULRVDULSZTUFWLVEVGUGWQUOZWFWRUPWSWQWFUQURUIWLV
      INZWLVJNZPWQWKPZWNWKXCWQXDWKXCVTWQKWQVOVDCVDULRWQVTWSUDUEXDWKWTVOVDDVFULR
      XATUFWLVIVJUGXBWKXEUPWSWQWKUQURUIUJUKUSVEVIJVGVJJVLVMVNVEVIVGVJUTACVDVABD
      VFVAVBVC $.
      $( [28-Sep-03] $)
  $}

  ${
    brelg.1 $e |- R (_ ( C X. D ) $.
    $( Two things in a binary relation belong to the relation's domain. $)
    brelg $p |- ( B e. S -> ( A R B -> ( A e. C /\ B e. D ) ) ) $=
      ( wcel cop wa wbr cxp opelxpg sseli syl5bi df-br syl5ib ) BFHZABIZEHZACHB
      DHJZABEKRSCDLZHUATABCDFMEUBSGNOABEPQ $.
      $( [17-May-96] $)
  $}

  ${
    brel.1 $e |- B e. V $.
    brel.2 $e |- R (_ ( C X. D ) $.
    $( Membership in superset of binary relation. $)
    brel $p |- ( A R B -> ( A e. C /\ B e. D ) ) $=
      ( cvv wcel wbr wa wi brelg ax-mp ) BHIABEJACIBDIKLFABCDEHGMN $.
      $( [16-Jul-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in a cross product. $)
    elxp3 $p |- ( A e. ( B X. C ) <->
            E. x E. y ( <. x , y >. = A /\ <. x , y >. e. ( B X. C ) ) ) $=
      ( cxp wcel cv cop wceq wa wex elxp cleqcom visset opelxp bi2an bi2ex
      bitr4 ) CDEFZGCAHZBHZIZJZUADGUBEGKZKZBLALUCCJZUCTGZKZBLALABCDEMUIUFABUGUD
      UHUEUCCNUAUBDEBOPQRS $.
      $( [5-Mar-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Distributive law for cross product over union.  Theorem 103 of [Suppes]
       p. 52. $)
    xpun1 $p |- ( A X. ( B u. C ) ) = ( ( A X. B ) u. ( A X. C ) ) $=
      ( vx vy cv wcel cun wa copab cxp wo elun bilan andi bitr biopab unopab
      eqtr4 df-xp uneq12i 3eqtr4 ) DFAGZEFZBCHZGZIZDEJZUCUDBGZIZDEJZUCUDCGZIZDE
      JZHZAUEKABKZACKZHUHUJUMLZDEJUOUGURDEUGUCUIULLZIURUFUSUCUDBCMNUCUIULOPQUJU
      MDERSDEAUETUPUKUQUNDEABTDEACTUAUB $.
      $( [13-Aug-04] $) $( [12-Aug-04] $)

    $( Distributive law for cross product over union.  Similar to Theorem 103
       of [Suppes] p. 52. $)
    xpun2 $p |- ( ( A u. B ) X. C ) = ( ( A X. C ) u. ( B X. C ) ) $=
      ( vx vy cv cun wcel wa copab cxp wo elun biran andir bitr biopab unopab
      eqtr4 df-xp uneq12i 3eqtr4 ) DFZABGZHZEFCHZIZDEJZUCAHZUFIZDEJZUCBHZUFIZDE
      JZGZUDCKACKZBCKZGUHUJUMLZDEJUOUGURDEUGUIULLZUFIURUEUSUFUCABMNUIULUFOPQUJU
      MDERSDEUDCTUPUKUQUNDEACTDEBCTUAUB $.
      $( [30-Sep-02] $)
  $}

  $( The cross product of two unions. $)
  xpun $p |- ( ( A u. B ) X. ( C u. D ) ) =
            ( ( ( A X. C ) u. ( A X. D ) ) u. ( ( B X. C ) u. ( B X. D ) ) ) $=
    ( cun cxp xpun1 xpun2 uneq12i un4 3eqtr ) ABEZCDEFLCFZLDFZEACFZBCFZEZADFZBD
    FZEZEOREPSEELCDGMQNTABCHABDHIOPRSJK $.
    $( [8-Sep-04] $) $( [12-Aug-04] $)

  ${
    $d x y A $.
    $( Membership in universal class of ordered pairs. $)
    elvv $p |- ( A e. ( V X. V ) <-> E. x E. y A = <. x , y >. ) $=
      ( cvv cxp wcel cv cop wceq wa wex elxp visset pm3.2i biantru bi2ex bitr4
      ) CDDEFCAGZBGZHIZRDFZSDFZJZJZBKAKTBKAKABCDDLTUDABUCTUAUBAMBMNOPQ $.
      $( [4-Jul-94] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( A cross product is included in the ordered pair universe.  Exercise
       3 of [TakeutiZaring] p. 25. $)
    xpss $p |- ( A X. B ) (_ ( V X. V ) $=
      ( vz vx vy cxp cvv cv cop wceq wcel wa wex pm3.26 19.22i elxp elvv
      3imtr4 ssriv ) CABFZGGFZCHZDHZEHZIJZUCAKUDBKLZLZEMZDMUEEMZDMUBTKUBUAKUHUI
      DUGUEEUEUFNOODEUBABPDEUBQRS $.
      $( [2-Aug-94] $)
  $}

  $( Intersection of binary relation with cross product. $)
  brinxp $p |- ( ( A e. C /\ B e. D ) ->
               ( A R B <-> A ( R i^i ( C X. D ) ) B ) ) $=
    ( wcel wa wbr cxp cin ibar cop wi wb opelxpi ibib mpbi df-br a1i bi2and
    elin ancom 3bitr syl6bbr bitrd ) ACFBDFGZABEHZUFUGGZABECDIZJZHZUFUGKUFUHABL
    ZUIFZULEFZGZUKUFUFUMUGUNUFUMMUFUFUMNMABCDOUFUMPQUGUNNUFABERSTUKULUJFUNUMGUO
    ABUJRULEUIUAUNUMUBUCUDUE $.
    $( [9-Mar-97] $)

  ${
    $d x y z A $.  $d x y z R $.  $( ` x y z ` are dummy. $)
    $( Intersection of well-ordering with cross product of its field. $)
    weinxp $p |- ( R We A <-> ( R i^i ( A X. A ) ) We A ) $=
      ( vx vy vz wfr cv wbr weq w3o wral wa cxp cin wwe wss c0 wceq wn wrex wi
      wal wel wb wcel ssel im2and adantr brinxp ancoms syl6 exp3a imp31 binegd
      biraldva birexdva pm5.74i bial df-fr 3bitr4 pm4.2i bi3ord bi2al r2al
      bi2an dfwe2 ) ABFZCGZDGZBHZCDIZVIVHBHZJZDAKCAKZLABAAMNZFZVHVIVOHZVKVIVHVO
      HZJZDAKCAKZLABOAVOOVGVPVNVTEGZAPZWAQRSZLZVLSZDWAKZCWATZUAZEUBWDVRSZDWAKZC
      WATZUAZEUBVGVPWHWLEWDWGWKWDWFWJCWAWDCEUCZLZWEWIDWAWNDEUCZLVLVRWDWMWOVLVRU
      DZWDWMWOWPWDWMWOLZVHAUEZVIAUEZLZWPWBWQWTUAWCWBWMWRWOWSWAAVHUFWAAVIUFUGUHW
      SWRWPVIVHAABUIUJZUKULUMUNUOUPUQURECDABUSECDAVOUSUTWTVMUAZDUBCUBWTVSUAZDUB
      CUBVNVTXBXCCDWTVMVSWTVJVQVKVKVLVRVHVIAABUIWTVKVAXAVBUQVCVMCDAAVDVSCDAAVDU
      TVECDABVFCDAVOVFUT $.
      $( [9-Mar-97] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( An abstraction relation is a subset of a related cross product. $)
    opabssxp $p |- { <. x , y >. | ( ( x e. A /\ y e. B ) /\ ph ) }
                   (_ ( A X. B ) $=
      ( cv wcel wa copab cxp pm3.26 ssopab2i df-xp sseqtr4 ) BFDGCFEGHZAHZBCIOB
      CIDEJPOBCOAKLBCDEMN $.
      $( [16-Jul-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ps $.
    optocl.1 $e |- D = ( B X. C ) $.
    optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    optocl.3 $e |- ( ( x e. B /\ y e. C ) -> ph ) $.
    $( Implicit substitution of class for ordered pair. $)
    optocl $p |- ( A e. D -> ps ) $=
      ( wcel cxp eleq2i cv cop wceq wa wex elxp3 visset opelxp sylbi syl5bi
      imp 19.23aivv ) EHLEFGMZLZBHUGEINUHCOZDOZPZEQZUKUGLZRZDSCSBCDEFGTUNBCDULU
      MBULABUMJUMUIFLUJGLRAUIUJFGDUAUBKUCUDUEUFUCUC $.
      $( [5-Mar-95] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y ps $.  $d z w ch $.  $d z w R $.
    2optocl.1 $e |- R = ( C X. D ) $.
    2optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    2optocl.3 $e |- ( <. z , w >. = B -> ( ps <-> ch ) ) $.
    2optocl.4 $e |- ( ( ( x e. C /\ y e. D ) /\ ( z e. C /\ w e. D ) ) ->
                   ph ) $.
    $( Implicit substitution of classes for ordered pairs. $)
    2optocl $p |- ( ( A e. R /\ B e. R ) -> ch ) $=
      ( wcel wi cv cop wceq bilimd wa exp optocl com12 imp ) HLQZILQZCUIUHCUHBR
      UHCRFGIJKLMFSZGSZTIUABCUHOUBUHUJJQUKKQUCZBULARULBRDEHJKLMDSZESZTHUAABULNU
      BUMJQUNKQUCULAPUDUEUFUEUFUG $.
      $( [12-Mar-95] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d v u C $.
    $d x y z w v u D $.  $d x y z w v u F $.  $d z w v u R $.
    $d x y ps $.  $d z w ch $.  $d v u th $.
    3optocl.1 $e |- R = ( D X. F ) $.
    3optocl.2 $e |- ( <. x , y >. = A -> ( ph <-> ps ) ) $.
    3optocl.3 $e |- ( <. z , w >. = B -> ( ps <-> ch ) ) $.
    3optocl.4 $e |- ( <. v , u >. = C -> ( ch <-> th ) ) $.
    3optocl.5 $e |- ( ( ( x e. D /\ y e. F ) /\ ( z e. D /\ w e. F )
                    /\ ( v e. D /\ u e. F ) ) -> ph ) $.
    $( Implicit substitution of classes for ordered pairs. $)
    3optocl $p |- ( ( A e. R /\ B e. R /\ C e. R ) -> th ) $=
      ( wcel wa wi cv cop wceq bilimd 3expa exp 2optocl com12 optocl imp 3impa
      ) KOUBZLOUBZMOUBZDUPUQUCZURDURUSDUSCUDUSDUDIJMNPOQIUEZJUEZUFMUGCDUSTUHUSU
      TNUBVAPUBUCZCVBAUDVBBUDVBCUDEFGHKLNPOQEUEZFUEZUFKUGABVBRUHGUEZHUEZUFLUGBC
      VBSUHVCNUBVDPUBUCZVENUBVFPUBUCZUCVBAVGVHVBAUAUIUJUKULUMULUNUO $.
      $( [12-Mar-95] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u S $.
    $d x y ph $.  $d z w v u ps $.
    opbrop.1 $e |- ( ( ( z = A /\ w = B ) /\ ( v = C /\ u = D ) ) ->
                     ( ph <-> ps ) ) $.
    opbrop.2 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. z E. w E. v E. u ( ( x = <. z , w >. /\
                      y = <. v , u >. ) /\ ph ) ) } $.
    $( Ordered pair membership in a relation.  Special case. $)
    opbrop $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                   ( <. A , B >. R <. C , D >. <-> ps ) ) $=
      ( wcel wa cop wbr cxp cv wceq wex copsex4g biland opex eleq1 birand
      cleq1 bi4exdv bi2and brab syl5bb opelxpi im2an biantrurd bitr4d ) INQJNQR
      ZKNQLNQRZRZIJSZKLSZMTZVBNNUAZQZVCVEQZRZBRZBVAVHVBEUBFUBSZUCZVCGUBHUBSZUCZ
      RZARZHUDGUDFUDEUDZRZVIVDVAVPBVHABEFGHIJKLNNOUEUFCUBZVEQZDUBZVEQZRZVRVJUCZ
      VTVLUCZRZARZHUDGUDFUDEUDZRVFWARZVKWDRZARZHUDGUDFUDEUDZRVQCDVBVCMIJUGKLUGV
      RVBUCZWBWHWGWKWLVSVFWAVRVBVEUHUIWLWFWJEFGHWLWEWIAWLWCVKWDVRVBVJUJUIUIUKUL
      VTVCUCZWHVHWKVPWMWAVGVFVTVCVEUHUFWMWJVOEFGHWMWIVNAWMWDVMVKVTVCVLUJUFUIUKU
      LPUMUNVAVHBUSVFUTVGIJNNUOKLNNUOUPUQUR $.
      $( [5-Aug-95] $)
  $}

  ${
    $d ph y z $.  $d ps x $.  $d x y z A $.  $d x y z B $.
    cbvop.1 $e |- ( x = <. y , z >. -> ( ph <-> ps ) ) $.
    $( Change restricted bound variable to two restricted bound variables. $)
    cbvop $p |- ( E. x e. ( A X. B ) ph <-> E. y e. A E. z e. B ps ) $=
      ( cv cxp wcel wa wex cop wrex wceq anass elxp3 eleq1 pm5.32i cleqcom
      biran bitr4 bi2ex 19.41vv 3bitr 3bitr4 biex exrot3 opex bi2and ceqsexv
      df-rex r2ex visset opelxp ) CIZFGJZKZALZCMZDIZEIZNZURKZBLZEMDMZACUROBEGOD
      FOZVAUQVDPZUTLZEMDMZCMVJCMZEMDMVGUTVKCVIEMDMZUSLZALVMUTLUTVKVMUSAQUSVNAUS
      VDUQPZVELZEMDMVIUSLZEMDMVNDEUQFGRVPVQDEVPVOUSLVQVOVEUSVDUQURSTVIVOUSUQVDU
      AUBUCUDVIUSDEUEUFUBVIUTDEUEUGUHVJCDEUIVLVFDEUTVFCVDVBVCUJVIUSVEABUQVDURSH
      UKULUDUFACURUMVHVBFKVCGKLZBLZEMDMVGBDEFGUNVFVSDEVEVRBVBVCFGEUOUPUBUDUCUG
      $.
      $( [11-Nov-95] $)
  $}

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( The cross product with the empty set is empty.  Part of Theorem 3.13(ii)
       of [Monk1] p. 37. $)
    xp0 $p |- ( (/) X. A ) = (/) $=
      ( vz vx vy c0 cxp cv wcel cop wceq wa wex elxp wn wb noel pm3.26 adantl
      mto nex pm5.21 mp2an bitr cleqri ) BEAFZEBGZUEHUFCGZDGZIJZUGEHZUHAHZKZKZD
      LZCLZUFEHZCDUFEAMUONUPNUOUPOUNCUMDUMUJUGPULUJUIUJUKQRSTTUFPUOUPUAUBUCUD
      $.
      $( [4-Jul-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The empty set is not a member of a cross product. $)
    0nelxp $p |- -. (/) e. ( A X. B ) $=
      ( vx vy c0 cxp wcel cv cop wceq wa wex csn noel opi1 eleq2 mpbiri mto
      intnanr nex elxp mtbir ) EABFGECHZDHZIZJZUCAGUDBGKZKZDLZCLUICUHDUFUGUFUCM
      ZEGZUJNUFUKUJUEGUCUDOEUEUJPQRSTTCDEABUAUB $.
      $( [2-May-96] $)
  $}

  ${
    $d x y z $.
    $( Ordinal numbers and ordered pairs are disjoint collections.  This
       theorem can be used if we want to extend a set of ordinal numbers or
       ordered pairs with disjoint elements.  See also ~ snsn0non . $)
    onxpdisj $p |- ( On i^i ( V X. V ) ) = (/) $=
      ( vx vy vz con0 cvv cxp cin c0 wceq cv wcel wn disj wo on0eqel 0nelxp
      eleq1 mtbiri cop wex elvv visset opprc1b bicon1 mpbir eleq2 19.23aivv
      sylbi con2i jaoi syl mprgbir ) DEEFZGHIAJZUMKZLZADADUMMUNDKUNHIZHUNKZNUPU
      NOUQUPURUQUOHUMKEEPUNHUMQRUOURUOUNBJZCJZSZIZCTBTURLZBCUNUAVBVCBCVBURHVAKZ
      VDLUSEKZBUBVEVDUSUTUCUDUEUNVAHUFRUGUHUIUJUKUL $.
      $( [2-Jun-04] $) $( [1-Jun-04] $)
  $}

  $( Equality theorem for relation predicate. $)
  releq $p |- ( A = B -> ( Rel A <-> Rel B ) ) $=
    ( wceq cvv cxp wss wrel sseq1 df-rel 3bitr4g ) ABCADDEZFBKFAGBGABKHAIBIJ $.
    $( [1-Aug-94] $)

  ${
    $d y A $.  $d x y $.
    hbrel.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for a relation. $)
    hbrel $p |- ( Rel A -> A. x Rel A ) $=
      ( cvv cxp wss wal wrel cv wcel ax-17 hbss df-rel bial 3imtr4 ) CEEFZGZRAH
      CIZSAHABCQDBJQKALMCNZSRATOP $.
      $( [31-Jan-04] $) $( [31-Jan-04] $)
  $}

  $( Subclass theorem for relation predicate.  Theorem 2 of [Suppes] p. 58. $)
  ssrel $p |- ( A (_ B -> ( Rel B -> Rel A ) ) $=
    ( wss cvv cxp wrel sstr df-rel 3imtr4g ) ABCBDDEZCAJCBFAFABJGBHAHI $.
    $( [15-Aug-94] $)

  ${
    $d x y z A $.  $d x y z B $.  $( ` z ` is dummy. $)
    $( A subclass relationship depends only on a relation's ordered pairs.
       Theorem 3.2(i) of [Monk1] p. 33. $)
    relss $p |- ( Rel A -> ( A (_ B <->
                A. x A. y ( <. x , y >. e. A -> <. x , y >. e. B ) ) ) $=
      ( vz wrel wss cv cop wcel wi wal ssel a1i 19.21adv wceq wex cvv cxp
      df-rel sylbi elvv syl6ib wa id imland eleq1 biimpar syl6 pm5.32i syl5ib
      exp3a 19.20i 19.23v sylib syl9 pm2.43 dfss2 syl6ibr impbid ) CFZCDGZAHBHI
      ZCJZVCDJZKZBLZALZVAVBVGAVAVBVFBVBVFKVACDVCMNOOVAVHEHZCJZVIDJZKZELVBVAVHVL
      EVAVHVJVLKVLVAVJVIVCPZBQZAQZVHVLVAVJVIRRSZJZVOVACVPGVJVQKCTCVPVIMUAABVIUB
      UCVHVNVLKZALVOVLKVGVRAVGVMVLKZBLVRVFVSBVFVMVJVKVFVMVDUDZVKVMVJUDVFVTVMVEU
      DVKVFVDVEVMVFUEUFVMVKVEVIVCDUGUHUIVMVJVDVIVCCUGUJUKULUMVMVLBUNUOUMVNVLAUN
      UOUPVJVKUQUIOECDURUSUT $.
      $( [2-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    relssi.1 $e |- Rel A $.
    relssi.2 $e |- ( <. x , y >. e. A -> <. x , y >. e. B ) $.
    $( Inference from subclass principle for relations. $)
    relssi $p |- A (_ B $=
      ( wss cv cop wcel wi wal wrel wb relss ax-mp ax-gen mpgbir ) CDGZAHBHIZCJ
      TDJKZBLZACMSUBALNEABCDOPUABFQR $.
      $( [31-Mar-98] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ph $.
    relssdv.1 $e |- ( ph -> Rel A ) $.
    relssdv.2 $e |- ( ph -> ( <. x , y >. e. A -> <. x , y >. e. B ) ) $.
    $( Deduction from subclass principle for relations. $)
    relssdv $p |- ( ph -> A (_ B ) $=
      ( wss cv cop wcel wi wal 19.21aivv wrel wb relss syl mpbird ) ADEHZBICIJZ
      DKUAEKLZCMBMZAUBBCGNADOTUCPFBCDEQRS $.
      $( [13-Sep-04] $) $( [11-Sep-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Extensionality principle for relations.  Theorem 3.2(ii) of [Monk1]
       p. 33. $)
    cleqrel $p |- ( ( Rel A /\ Rel B ) -> ( A = B <->
                A. x A. y ( <. x , y >. e. A <-> <. x , y >. e. B ) ) ) $=
      ( wrel wa wss cv cop wcel wi wal wceq wb relss bi2anan9 eqss albi bial
      19.26 bitr 3bitr4g ) CEZDEZFCDGZDCGZFAHBHIZCJZUGDJZKBLZALZUIUHKBLZALZFZCD
      MUHUINBLZALZUCUEUKUDUFUMABCDOABDCOPCDQUPUJULFZALUNUOUQAUHUIBRSUJULATUAUB
      $.
      $( [2-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    cleqreli.1 $e |- Rel A $.
    cleqreli.2 $e |- Rel B $.
    cleqreli.3 $e |- ( <. x , y >. e. A <-> <. x , y >. e. B ) $.
    $( Inference from extensionality principle for relations. $)
    cleqreli $p |- A = B $=
      ( wceq cv cop wcel wb wal wrel cleqrel mp2an ax-gen mpgbir ) CDHZAIBIJZCK
      TDKLZBMZACNDNSUBAMLEFABCDOPUABGQR $.
      $( [17-Mar-95] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    relsn.1 $e |- A e. V $.
    $( A singleton of an ordered pair is a relation. $)
    relsn $p |- Rel { <. A , B >. } $=
      ( cop csn wrel cvv cxp wss wcel opelxpi mpan wn wa pm3.2i opelxp mpbir
      opprc2 eleq1d mpbiri pm2.61i snssi ax-mp df-rel ) ABDZEZFUFGGHZIZUEUGJZUH
      BGJZUIAGJZUJUICABGGKLUJMZUIAADZUGJZUNUKUKNUKUKCCOAAGGCPQULUEUMUGABRSTUAUE
      UGUBUCUFUDQ $.
      $( [17-May-98] $)
  $}

  $( A cross product is a relation.  Theorem 3.13(i) of [Monk1] p. 37. $)
  relxp $p |- Rel ( A X. B ) $=
    ( cxp wrel cvv wss xpss df-rel mpbir ) ABCZDJEECFABGJHI $.
    $( [2-Aug-94] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
         $( ` x y ` are dummy. $)
    $( Subset theorem for cross product.  Generalization of Theorem 101 of
       [Suppes] p. 52. $)
    ssxp $p |- ( ( A (_ B /\ C (_ D ) -> ( A X. C ) (_ ( B X. D ) ) $=
      ( vx vy wss wa cxp wrel relxp a1i cv wcel wi cop prth visset opelxp
      3imtr4g ssel syl2an relssdv ) ABGZCDGZHZEFACIZBDIZUGJUFACKLEMZANZUIBNZOZF
      MZCNZUMDNZOZUIUMPZUGNZUQUHNZOUDUEULUPHUJUNHUKUOHURUSUJUKUNUOQUIUMACFRZSUI
      UMBDUTSTABUIUACDUMUAUBUC $.
      $( [26-Aug-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    xpex.1 $e |- A e. V $.
    xpex.2 $e |- B e. V $.
    $( The cross product of two sets is a set.  Proposition 6.2 of
       [TakeutiZaring] p. 23. $)
    xpex $p |- ( A X. B ) e. V $=
      ( vx vy cxp cun cpw unex pwex relxp cv cop wcel wa visset opelxp csn cpr
      wss snssi ssun3 syl snex elpw sylibr adantr ssun4 im2an unss sylib df-pr
      syl5ss zfpair jca prex df-op eleq1i prss 3bitr4r sylbi relssi ssexi ) ABG
      ZABHZIZIZVGVFABCDJKKEFVEVHABLEMZFMZNZVEOVIAOZVJBOZPZVKVHOZVIVJABFQRVNVISZ
      VGOZVIVJTZVGOZPZVOVNVQVSVLVQVMVLVPVFUAZVQVLVPAUAWAVIAUBVPABUCUDZVPVFVIUEZ
      UFUGUHVNVRVFUAVSVNVPVJSZHZVFVRVNWAWDVFUAZPWEVFUAVLWAVMWFWBVMWDBUAWFVJBUBW
      DBAUIUDUJVPWDVFUKULVIVJUMUNVRVFEFUOZUFUGUPVPVRTZVHOWHVGUAVOVTWHVGVPVRUQUF
      VKWHVHVIVJURUSVPVRVGWCWGUTVAULVBVCVD $.
      $( [14-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $( ` x y ` are dummy. $)
    $( The cross product of two sets is a set.  Generalization of Proposition
       6.2 of [TakeutiZaring] p. 23. $)
    xpexg $p |- ( ( A e. C /\ B e. D ) -> ( A X. B ) e. V ) $=
      ( vx vy cv cxp cvv wcel wceq xpeq1 eleq1d xpeq2 visset xpex vtocl2g ) EGZ
      FGZHZIJASHZIJABHZIJEFABCDRAKTUAIRASLMSBKUAUBISBANMRSEOFOPQ $.
      $( [14-Aug-94] $)
  $}

  $( The union of two relations is a relation.  Compare Exercise 5 of
     [TakeutiZaring] p. 25. $)
  relun $p |- ( Rel ( A u. B ) <-> ( Rel A /\ Rel B ) ) $=
    ( cvv cxp wss wa cun wrel unss df-rel bi2an 3bitr4r ) ACCDZEZBMEZFABGZMEAHZ
    BHZFPHABMIQNROAJBJKPJL $.
    $( [12-Aug-94] $)

  $( The intersection with a relation is a relation. $)
  relin $p |- ( Rel A -> Rel ( A i^i B ) ) $=
    ( cin wss wrel wi inss1 ssrel ax-mp ) ABCZADAEJEFABGJAHI $.
    $( [16-Aug-94] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.
        $( ` x y ` are dummy. $)
    $( Distributive law for cross products.  Exercise 9 of [TakeutiZaring]
       p. 25. $)
    xpindi $p |- ( ( A X. B ) i^i ( C X. D ) ) =
                   ( ( A i^i C ) X. ( B i^i D ) ) $=
      ( vx vy cxp cin wrel relxp relin ax-mp cv cop wcel wa an4 visset opelxp
      bi2an elin 3bitr4 cleqreli ) EFABGZCDGZHZACHZBDHZGZUDIUFIABJUDUEKLUGUHJEM
      ZFMZNZUDOZULUEOZPZUJUGOZUKUHOZPZULUFOULUIOUJAOZUKBOZPZUJCOZUKDOZPZPUSVBPZ
      UTVCPZPUOURUSUTVBVCQUMVAUNVDUJUKABFRZSUJUKCDVGSTUPVEUQVFUJACUAUKBDUATUBUL
      UDUEUAUJUKUGUHVGSUBUC $.
      $( [3-Aug-94] $)

    $( Distributive law for cross products.  Exercise 9 of [TakeutiZaring]
       p. 25. $)
    xpindir $p |- ( ( A X. B ) i^i ( C X. D ) ) =
                   ( ( A i^i C ) X. ( B i^i D ) ) $=
      ( vx vy cxp cin wrel relxp relin ax-mp cv cop wcel wa an4 visset opelxp
      bi2an elin 3bitr4 cleqreli ) EFABGZCDGZHZACHZBDHZGZUDIUFIABJUDUEKLUGUHJEM
      ZFMZNZUDOZULUEOZPZUJUGOZUKUHOZPZULUFOULUIOUJAOZUKBOZPZUJCOZUKDOZPZPUSVBPZ
      UTVCPZPUOURUSUTVBVCQUMVAUNVDUJUKABFRZSUJUKCDVGSTUPVEUQVFUJACUAUKBDUATUBUL
      UDUEUAUJUKUGUHVGSUBUC $.
      $( [3-Aug-94] $)
  $}

  $( A difference cutting down a relation is a relation. $)
  reldif $p |- ( Rel A -> Rel ( A \ B ) ) $=
    ( cdif wss wrel wi difss ssrel ax-mp ) ABCZADAEJEFABGJAHI $.
    $( [31-Mar-98] $)

  ${
    $d x y A $.
    $( Union law for relations.  Exercise 6 of [TakeutiZaring] p. 25 and its
       converse. $)
    reluni $p |- ( Rel U. A <-> A. x e. A Rel x ) $=
      ( vy wel cv cvv cxp wcel wi wral wal cuni wrel wrex r19.23v eluni2 birim
      bitr4 bial wss df-rel dfss2 bitr biral ralcom4 3bitr4r ) CADZCEZFFGZHZIZA
      BJZCKZUHBLZHZUJIZCKZAEZMZABJZUNMZULUPCULUGABNZUJIUPUGUJABOUOVBUJAUHBPQRSU
      TUKCKZABJUMUSVCABUSURUITVCURUACURUIUBUCUDUKACBUEUCVAUNUITUQUNUACUNUIUBUCU
      F $.
      $( [14-Aug-04] $) $( [13-Aug-04] $)
  $}

  ${
    $d x y z $.  $d z ph $.  $( ` z ` is dummy. $)
    $( A class of ordered pairs is a relation. $)
    relopab $p |- Rel { <. x , y >. | ph } $=
      ( vz copab wrel cvv cxp wss cv cop wceq wa wex wcel pm3.26 19.22i
      df-opab cleqabi elvv 3imtr4 ssriv df-rel mpbir ) ABCEZFUEGGHZIDUEUFDJZBJC
      JKLZAMZCNZBNZUHCNZBNUGUEOUGUFOUJULBUIUHCUHAPQQUKDUEABCDRSBCUGTUAUBUEUCUD
      $.
      $( [8-Mar-95] $)
  $}

  ${
    $d x y z w $.  $d ph z w $.  $d ps z w $.
    $( Intersection of two ordered pair class abstractions. $)
    inopab $p |- ( { <. x , y >. | ph } i^i { <. x , y >. | ps } ) =
               { <. x , y >. | ( ph /\ ps ) } $=
      ( vz vw copab cin wa wrel relopab relin ax-mp cv cop wcel wsb sban bisb
      opabsb bi2an 3bitr4r elin 3bitr4 cleqreli ) EFACDGZBCDGZHZABIZCDGZUFJUHJA
      CDKUFUGLMUICDKENFNOZUFPZUKUGPZIZUICEQZDFQZUKUHPUKUJPACEQZBCEQZIZDFQUQDFQZ
      URDFQZIUPUNUQURDFRUOUSDFABCERSULUTUMVAACDEFTBCDEFTUAUBUKUFUGUCUICDEFTUDUE
      $.
      $( [30-Sep-02] $)
  $}

  $( The empty set is a relation. $)
  rel0 $p |- Rel (/) $=
    ( c0 wrel cvv cxp wss 0ss df-rel mpbir ) ABACCDZEIFAGH $.
    $( [26-Apr-98] $)

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The identity relation is a relation.  Part of Exercise 4.12(p) of
       [Mendelson] p. 235. $)
    reli $p |- Rel I $=
      ( vx vy cid wrel weq copab relopab wceq wb df-id releq ax-mp mpbir ) CDZA
      BEZABFZDZOABGCPHNQIABJCPKLM $.
      $( [26-Apr-98] $)

    $( The membership relation is a relation. $)
    rele $p |- Rel E $=
      ( vx vy cep wrel wel copab relopab wceq wb df-eprel releq ax-mp mpbir )
      CDZABEZABFZDZOABGCPHNQIABJCPKLM $.
      $( [26-Apr-98] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $( ` x y z ` are dummy. $)
    $( Equality theorem for composition of two classes. $)
    coeq1 $p |- ( A = B -> ( A o. C ) = ( B o. C ) ) $=
      ( vx vz vy wceq cv wbr wa wex copab ccom breq biland biexdv biopabdv
      df-co 3eqtr4g ) ABGZDHEHZCIZUAFHZAIZJZEKZDFLUBUAUCBIZJZEKZDFLACMBCMTUFUID
      FTUEUHETUDUGUBUAUCABNOPQDFEACRDFEBCRS $.
      $( [3-Jan-97] $)

    $( Equality theorem for composition of two classes. $)
    coeq2 $p |- ( A = B -> ( C o. A ) = ( C o. B ) ) $=
      ( vx vz vy wceq cv wbr wa wex copab ccom breq birand biexdv biopabdv
      df-co 3eqtr4g ) ABGZDHZEHZAIZUBFHCIZJZEKZDFLUAUBBIZUDJZEKZDFLCAMCBMTUFUID
      FTUEUHETUCUGUDUAUBABNOPQDFECARDFECBRS $.
      $( [3-Jan-97] $)
  $}

  ${
    coeq1i.1 $e |- A = B $.
    $( Equality inference for composition of two classes. $)
    coeq1i $p |- ( A o. C ) = ( B o. C ) $=
      ( wceq ccom coeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [16-Nov-00] $)

    $( Equality inference for composition of two classes. $)
    coeq2i $p |- ( C o. A ) = ( C o. B ) $=
      ( wceq ccom coeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [16-Nov-00] $)
  $}

  ${
    coeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for composition of two classes. $)
    coeq1d $p |- ( ph -> ( A o. C ) = ( B o. C ) ) $=
      ( wceq ccom coeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [16-Nov-00] $)

    $( Equality deduction for composition of two classes. $)
    coeq2d $p |- ( ph -> ( C o. A ) = ( C o. B ) ) $=
      ( wceq ccom coeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [16-Nov-00] $)
  $}

  ${
    $d y z w v A $.  $d y z w v B $.  $d x y z w v $.
        $( ` z w v ` are dummy. $)
    hbco.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbco.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for function value. $)
    hbco $p |- ( y e. ( A o. B ) -> A. x y e. ( A o. B ) ) $=
      ( vz vv vw cv wbr wa wex copab wcel wal ccom wel ax-17 hbbr hban hbex
      hbopab df-co eleq2i bial 3imtr4 ) BJZGJZHJZDKZUJIJZCKZLZHMZGINZOZUQAPUHCD
      QZOZUSAPUOGIABUNAHUKUMAABUIUJDBGRASFBHRASZTABUJULCUTEBIRASTUAUBUCURUPUHGI
      HCDUDUEZUSUQAVAUFUG $.
      $( [1-Sep-99] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
      $( ` y z ` are dummy. $)
    opelco.1 $e |- A e. V $.
    opelco.2 $e |- B e. V $.
    $( Ordered pair membership in a composition. $)
    opelco $p |- ( <. A , B >. e. ( C o. D ) <-> E. x ( A D x /\ x C B ) ) $=
      ( vy vz cop ccom wcel cv wbr wa wex copab df-co eleq2i wceq breq1 birand
      biexdv breq2 biland opelopab bitr ) BCJZDEKZLUHHMZAMZENZUKIMZDNZOZAPZHIQZ
      LBUKENZUKCDNZOZAPZUIUQUHHIADERSUPURUNOZAPVAHIBCFGUJBTZUOVBAVCULURUNUJBUKE
      UAUBUCUMCTZVBUTAVDUNUSURUMCUKDUDUEUCUFUG $.
      $( [27-Dec-96] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d z R $.
      $( ` y z ` are dummy. $)
    $( Ordered pair membership in a composition. $)
    opelcog $p |- ( ( A e. R /\ B e. S ) -> ( <. A , B >. e. ( C o. D ) <->
                   E. x ( <. A , x >. e. D /\ <. x , B >. e. C ) ) ) $=
      ( vy vz wcel wa cop ccom cv wbr wex wb wceq opeq1 eleq1d breq1 birand
      biexdv bi2bid opeq2 breq2 biland visset opelco vtocl2g df-br bi2an biex
      syl6bb ) BFJCGJKBCLZDEMZJZBANZEOZURCDOZKZAPZBURLEJZURCLDJZKZAPHNZINZLZUPJ
      ZVFUREOZURVGDOZKZAPZQBVGLZUPJZUSVKKZAPZQUQVBQHIBCFGVFBRZVIVOVMVQVRVHVNUPV
      FBVGSTVRVLVPAVRVJUSVKVFBUREUAUBUCUDVGCRZVOUQVQVBVSVNUOUPVGCBUETVSVPVAAVSV
      KUTUSVGCURDUFUGUCUDAVFVGDEHUHIUHUIUJVAVEAUSVCUTVDBUREUKURCDUKULUMUN $.
      $( [27-Jan-97] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Subset theorem for converse. $)
    cnvss $p |- ( A (_ B -> `' A (_ `' B ) $=
      ( vy vx wss cv wbr copab ccnv wi wal cop wcel ssel df-br 3imtr4g
      19.21aivv ssopab2 sylibr df-cnv 3sstr4g ) ABEZCFZDFZAGZDCHZUCUDBGZDCHZAIB
      IUBUEUGJZCKDKUFUHEUBUIDCUBUCUDLZAMUJBMUEUGABUJNUCUDAOUCUDBOPQUEUGDCRSDCAT
      DCBTUA $.
      $( [22-Mar-98] $)
  $}

  $( Equality theorem for converse. $)
  cnveq $p |- ( A = B -> `' A = `' B ) $=
    ( wss wa ccnv wceq cnvss im2an eqss 3imtr4 ) ABCZBACZDAEZBEZCZNMCZDABFMNFKO
    LPABGBAGHABIMNIJ $.
    $( [13-Aug-95] $)

  ${
    $d x y A $.  $d x y R $.
    $( Membership in a converse.  Equation (5) of [Suppes] p. 62. $)
    elcnv $p |- ( A e. `' R <-> E. x E. y ( A = <. x , y >. /\ y R x ) ) $=
      ( ccnv wcel cv wbr copab cop wceq wa wex df-cnv eleq2i elopab bitr ) CDEZ
      FCBGZAGZDHZABIZFCTSJKUALBMAMRUBCABDNOUAABCPQ $.
      $( [24-Mar-98] $)

    $( Membership in a converse.  Equation (5) of [Suppes] p. 62. $)
    elcnv2 $p |- ( A e. `' R <->
                 E. x E. y ( A = <. x , y >. /\ <. y , x >. e. R ) ) $=
      ( ccnv wcel cv cop wceq wbr wa wex elcnv df-br bilan bi2ex bitr ) CDEFCAG
      ZBGZHIZSRDJZKZBLALTSRHDFZKZBLALABCDMUBUDABUAUCTSRDNOPQ $.
      $( [8-Sep-04] $) $( [11-Aug-04] $)
  $}

  ${
    $d y z w A $.  $d x y z w $.
    hbcnv.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for converse. $)
    hbcnv $p |- ( y e. `' A -> A. x y e. `' A ) $=
      ( vz vw cv cop wceq wbr wa wex wal ccnv wcel ax-17 wel hbbr hban hbex
      elcnv bial 3imtr4 ) BGZEGZFGZHIZUFUECJZKZFLZELZUKAMUDCNOZULAMUJAEUIAFUGUH
      AUGAPABUFUECBFQAPDBEQAPRSTTEFUDCUAZULUKAUMUBUC $.
      $( [31-Jan-04] $) $( [31-Jan-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $( ` x y ` are dummy. $)
    $( Ordered-pair membership in converse. $)
    opelcnvg $p |- ( ( A e. C /\ B e. D ) ->
                     ( <. A , B >. e. `' R <-> <. B , A >. e. R ) ) $=
      ( vy vx wcel wa cop ccnv wbr cv copab breq2 breq1 opelopabg df-cnv
      eleq2i syl5bb df-br syl6bb ) ACHBDHIZABJZEKZHZBAELZBAJEHUCUDFMZGMZELZGFNZ
      HUGUFUJUHAELUGGFABCDUIAUHEOUHBAEPQUEUKUDGFERSTBAEUAUB $.
      $( [13-May-99] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $( ` x y ` are dummy. $)
    opelcnv.1 $e |- A e. V $.
    opelcnv.2 $e |- B e. V $.
    $( Ordered-pair membership in converse. $)
    opelcnv $p |- ( <. A , B >. e. `' R <-> <. B , A >. e. R ) $=
      ( cvv wcel cop ccnv wb opelcnvg mp2an ) AFGBFGABHCIGBAHCGJDEABFFCKL $.
      $( [13-Aug-95] $)
  $}

  ${
    brcnv.1 $e |- A e. V $.
    brcnv.2 $e |- B e. V $.
    $( The converse of a binary relation swaps arguments.  Theorem 11 of
       [Suppes] p. 61.  $)
    brcnv $p |- ( A `' R B <-> B R A ) $=
      ( cop ccnv wcel wbr opelcnv df-br 3bitr4 ) ABFCGZHBAFCHABMIBACIABCDEJABMK
      BACKL $.
      $( [13-Aug-95] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Distributive law of converse over class composition.  Theorem 26 of
       [Suppes] p. 64. $)
    cnvco $p |- `' ( A o. B ) = ( `' B o. `' A ) $=
      ( vx vy vz cv ccom wbr copab ccnv wa wex cop wcel df-br visset opelco
      ancom brcnv bi2an bitr4 biex 3bitr biopab df-cnv df-co 3eqtr4 ) CFZDFZABG
      ZHZDCIUIEFZAJZHZULUHBJZHZKZELZDCIUJJUOUMGUKURDCUKUHUIMUJNUHULBHZULUIAHZKZ
      ELURUHUIUJOEUHUIABCPZDPZQVAUQEVAUTUSKUQUSUTRUNUTUPUSUIULAVCEPZSULUHBVDVBS
      TUAUBUCUDDCUJUEDCEUOUMUFUG $.
      $( [19-Mar-98] $)
  $}

  ${
    $d x y z w A $.
    $( The converse of a class union is the (indexed) union of the converses of
       its members. $)
    cnvuni $p |- `' U. A = U. x e. A `' x $=
      ( vy vz vw cuni ccnv cv ciun wcel wrex cop wceq wa wex elcnv2 eluni2
      bilan r19.42v bitr4 bi2ex rexcom4 biex bitr2 birex 3bitr eliun cleqri )
      CBFZGZABAHZGZIZCHZUJJZUNULJZABKZUNUMJUOUNDHZEHZLMZUSURLZUIJZNZEODOUTVAUKJ
      ZNZABKZEOZDOZUQDEUNUIPVCVFDEVCUTVDABKZNVFVBVIUTAVABQRUTVDABSTUAVHVEEOZDOZ
      ABKZUQVLVJABKZDOVHVJADBUBVMVGDVEAEBUBUCUDUPVKABDEUNUKPUETUFAUNBULUGTUH $.
      $( [14-Sep-04] $) $( [11-Aug-04] $)
  $}

  ${
    $d x y A $.
    $( Alternate definition of domain.  Definition 6.5(1) of [TakeutiZaring]
       p. 24. $)
    dfdm3 $p |- dom A = { x | E. y <. x , y >. e. A } $=
      ( cdm cv wbr wex cab cop wcel df-dm df-br biex biabi eqtr ) CDAEZBEZCFZBG
      ZAHPQICJZBGZAHABCKSUAARTBPQCLMNO $.
      $( [28-Dec-96] $)

    $( Alternate definition of range.  Definition 4 of [Suppes] p. 60. $)
    dfrn2 $p |- ran A = { y | E. x x A y } $=
      ( crn ccnv cdm cv wbr wex cab df-rn df-dm visset brcnv biex biabi 3eqtr
      ) CDCEZFBGZAGZRHZAIZBJTSCHZAIZBJCKBARLUBUDBUAUCASTCBMAMNOPQ $.
      $( [27-Dec-96] $)

    $( Alternate definition of range. Definition 6.5(2) of [TakeutiZaring]
       p. 24. $)
    dfrn3 $p |- ran A = { y | E. x <. x , y >. e. A } $=
      ( crn cv wbr wex cab cop wcel dfrn2 df-br biex biabi eqtr ) CDAEZBEZCFZAG
      ZBHPQICJZAGZBHABCKSUABRTAPQCLMNO $.
      $( [28-Dec-96] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( Alternate definition of domain. $)
    dfdm4 $p |- dom A = ran `' A $=
      ( vy vx cv ccnv wbr wex cab crn cdm visset brcnv biex biabi dfrn2 df-dm
      3eqtr4r ) BDZCDZAEZFZBGZCHSRAFZBGZCHTIAJUBUDCUAUCBRSABKCKLMNBCTOCBAPQ $.
      $( [28-Dec-96] $)
  $}

  ${
    $d x y z w v $.  $d z w v A $.  $( ` w v ` are dummy. $)
    dfdmf.1 $e |- ( z e. A -> A. x z e. A ) $.
    dfdmf.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Domain definition requiring only that ` x ` and ` y ` not be
       'free' in ` A ` (but not necessarily absent from it). $)
    dfdmf $p |- dom A = { x | E. y <. x , y >. e. A } $=
      ( vw vv cdm cv cop wcel wex cab dfdm3 ax-17 hbel weq opeq2 eleq1d cbvex
      biabi hbex opeq1 biexdv cbvab 3eqtr ) DIGJZHJZKZDLZHMZGNUHBJZKZDLZBMZGNAJ
      ZUMKZDLZBMZANGHDOULUPGUKUOHBBCCUJDCJZUJLBPFQUOHPHBRUJUNDUIUMUHSTUAUBUPUTG
      AUOABACCUNDVAUNLAPEQUCUTGPGARZUOUSBVBUNURDUHUQUMUDTUEUFUG $.
      $( [8-Mar-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x ` is dummy. $)
    eldm.1 $e |- A e. V $.
    $( Membership in a domain.  Theorem 4 of [Suppes] p. 59. $)
    eldm $p |- ( A e. dom B <-> E. y A B y ) $=
      ( vx cv wbr wex cdm wceq breq1 biexdv df-dm elab2 ) EFZAFZCGZAHBPCGZAHEBC
      IDOBJQRAOBPCKLEACMN $.
      $( [5-Apr-04] $) $( [2-Apr-04] $)

    $( Membership in a domain.  Theorem 4 of [Suppes] p. 59. $)
    eldm2 $p |- ( A e. dom B <-> E. y <. A , y >. e. B ) $=
      ( cdm wcel cv wbr wex cop eldm df-br biex bitr ) BCEFBAGZCHZAIBOJCFZAIABC
      DKPQABOCLMN $.
      $( [1-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x ` is dummy. $)
    $( Domain membership.  Theorem 4 of [Suppes] p. 59. $)
    eldmg $p |- ( A e. C -> ( A e. dom B <-> E. y <. A , y >. e. B ) ) $=
      ( vx cv cdm wcel cop wex eleq1 wceq opeq1 eleq1d biexdv visset eldm2
      vtoclbg ) EFZCGZHSAFZIZCHZAJBTHBUAIZCHZAJEBDSBTKSBLZUCUEAUFUBUDCSBUAMNOAS
      CEPQR $.
      $( [27-Jan-97] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Subset theorem for domain. $)
    dmss $p |- ( A (_ B -> dom A (_ dom B ) $=
      ( vx vy wss cdm cv cop wcel wex ssel 19.22dv visset eldm2 3imtr4g ssrdv
      ) ABEZCAFZBFZQCGZDGHZAIZDJUABIZDJTRITSIQUBUCDABUAKLDTACMZNDTBUDNOP $.
      $( [11-Aug-94] $)
  $}

  $( Equality theorem for domain. $)
  dmeq $p |- ( A = B -> dom A = dom B ) $=
    ( wss wa cdm wceq dmss im2an eqss 3imtr4 ) ABCZBACZDAEZBEZCZNMCZDABFMNFKOLP
    ABGBAGHABIMNIJ $.
    $( [11-Aug-94] $)

  ${
    dmeqi.1 $e |- A = B $.
    $( Equality inference for domain. $)
    dmeqi $p |- dom A = dom B $=
      ( wceq cdm dmeq ax-mp ) ABDAEBEDCABFG $.
      $( [5-Mar-04] $) $( [4-Mar-04] $)
  $}

  ${
    dmeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for domain. $)
    dmeqd $p |- ( ph -> dom A = dom B ) $=
      ( wceq cdm dmeq syl ) ABCEBFCFEDBCGH $.
      $( [5-Mar-04] $) $( [4-Mar-04] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y C $.  $( ` y ` is dummy. $)
    opeldm.1 $e |- A e. V $.
    $( Membership of first of an ordered pair in a domain. $)
    opeldm $p |- ( <. A , B >. e. C -> A e. dom C ) $=
      ( vy cvv wcel cop cdm wi cv wex wceq opeq2 eleq1d cla4egv eldm2 syl6ibr
      wn opprc2 cla4ev sylibr syl6bi pm2.61i ) BFGZABHZCGZACIGZJUEUGAEKZHZCGZEL
      ZUHUKUGEBFUIBMUJUFCUIBANOPEACDQZRUESZUGAAHZCGZUHUNUFUOCABTOUPULUHUKUPEADU
      IAMUJUOCUIAANOUAUMUBUCUD $.
      $( [30-Jul-95] $)
  $}

  ${
    breldm.1 $e |- A e. V $.
    $( Membership of first of a binary relation in a domain. $)
    breldm $p |- ( A R B  -> A e. dom R ) $=
      ( wbr cop wcel cdm df-br opeldm sylbi ) ABCEABFCGACHGABCIABCDJK $.
      $( [30-Jul-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The domain of a union is the union of domains.  Exercise 56(a) of
       [Enderton] p. 65. $)
    dmun $p |- dom ( A u. B ) = ( dom A u. dom B ) $=
      ( vx vy cun cdm cv cop wcel wex wo elun biex 19.43 bitr visset eldm2
      bi2or 3bitr4 cleqri ) CABEZFZAFZBFZEZCGZDGHZUAIZDJZUGAIZDJZUGBIZDJZKZUFUB
      IUFUEIZUIUJULKZDJUNUHUPDUGABLMUJULDNODUFUACPZQUOUFUCIZUFUDIZKUNUFUCUDLURU
      KUSUMDUFAUQQDUFBUQQROST $.
      $( [12-Aug-94] $)

    $( The domain of an intersection belong to the intersection of domains.
       Theorem 6 of [Suppes] p. 60. $)
    dmin $p |- dom ( A i^i B ) (_ ( dom A i^i dom B ) $=
      ( vx vy cin cdm cv cop wcel wa wex 19.40 visset eldm2 elin biex bitr
      bi2an 3imtr4 ssriv ) CABEZFZAFZBFZEZCGZDGHZAIZUGBIZJZDKZUHDKZUIDKZJZUFUBI
      ZUFUEIZUHUIDLUOUGUAIZDKUKDUFUACMZNUQUJDUGABOPQUPUFUCIZUFUDIZJUNUFUCUDOUSU
      LUTUMDUFAURNDUFBURNRQST $.
      $( [17-Sep-04] $) $( [15-Sep-04] $)

  $}

  ${
    $d x y z A $.
    $( The domain of a union.  Part of Exercise 8 of [Enderton] p. 41. $)
    dmuni $p |- dom U. A = U. x e. A dom x $=
      ( vy vz cuni cdm cv ciun cop wcel wex wrex wa eluni biex excom ancom
      19.41v visset eldm2 bilan 3bitr4 3bitr df-rex bitr4 eliun cleqri ) CBEZFZ
      ABAGZFZHZCGZDGIZUHJZDKZUMUKJZABLZUMUIJUMULJUPUJBJZUQMZAKZURUPUNUJJZUSMZAK
      ZDKVCDKZAKVAUOVDDAUNBNOVCDAPVEUTAVBDKZUSMUSVFMVEUTVFUSQVBUSDRUQVFUSDUMUJC
      SZTUAUBOUCUQABUDUEDUMUHVGTAUMBUKUFUBUG $.
      $( [13-Mar-04] $) $( [3-Feb-04] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( The domain of a class of ordered pairs. $)
    dmopab $p |- dom { <. x , y >. | ph } = { x | E. y ph } $=
      ( vz copab cdm cv cop wcel wex cab hbopab1 hbopab2 dfdmf opabid biex
      biabi eqtr ) ABCEZFBGCGHSIZCJZBKACJZBKBCDSABCDLABCDMNUAUBBTACABCOPQR $.
      $( [16-May-95] $)
  $}

  ${
    $d x y A $.
    $( Upper bound for the domain of a restricted class of ordered pairs. $)
    dmopabss $p |- dom { <. x , y >. | ( x e. A /\ ph ) } (_ A $=
      ( cv wcel wa copab cdm wex cab dmopab 19.42v biabi ssab eqsstr ) BEDFZAGZ
      BCHIRCJZBKZDRBCLTQACJZGZBKDSUBBQACMNUABDOPP $.
      $( [1-Feb-04] $) $( [31-Jan-04] $)
  $}

  ${
    $d x y A $.
    $( The domain of a restricted class of ordered pairs. $)
    dmopab2 $p |- ( A. x e. A E. y ph <->
               dom { <. x , y >. | ( x e. A /\ ph ) } = A ) $=
      ( wex wral cv wcel wi wal wa wb copab cdm wceq df-ral pm4.71 bial cab
      cleqab cleqcom dmopab 19.42v biabi eqtr cleq1i bitr4 bitr3 3bitr ) ACEZBD
      FBGDHZUJIZBJUKUKUJKZLZBJZUKAKZBCMNZDOZUJBDPULUNBUKUJQRUODUMBSZOZURUMBDTUT
      USDOURDUSUAUQUSDUQUPCEZBSUSUPBCUBVAUMBUKACUCUDUEUFUGUHUI $.
      $( [1-Feb-04] $) $( [31-Jan-04] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The domain of the empty set is empty.  Part of Theorem 3.8(v) of [Monk1]
       p. 36. $)
    dm0 $p |- dom (/) = (/) $=
      ( vx vy cv cop c0 wcel wex cab weq wn cdm wb noel nex cleqid negb ax-mp
      pm5.21 mp2an biabi dfdm3 dfnul2 3eqtr4 ) ACZBCDZEFZBGZAHAAIZJZAHEKEUGUIAU
      GJUIJZUGUILUFBUEMNUHUJUDOUHPQUGUIRSTABEUAAUBUC $.
      $( [4-Jul-94] $)

    $( The domain of the singleton of the empty set is empty. $)
    dmsn0 $p |- dom { (/) } = (/) $=
      ( vx vy cv cop c0 csn wcel wex cab weq wn cdm wb wceq opnz opex elsnc
      mtbir nex cleqid negb ax-mp pm5.21 mp2an biabi dfdm3 dfnul2 3eqtr4 ) ACZB
      CZDZEFZGZBHZAIAAJZKZAIULLEUNUPAUNKUPKZUNUPMUMBUMUKENUIUJOUKEUIUJPQRSUOUQU
      ITUOUAUBUNUPUCUDUEABULUFAUGUH $.
      $( [16-Feb-04] $) $( [30-Jan-04] $)

    $( The domain of the singleton of the singleton of the empty set is
       empty. $)
    dmsnsn0 $p |- dom { { (/) } } = (/) $=
      ( vx vy cv cop c0 csn wcel wex cab weq wn cdm wb wceq cvv wa wo visset
      a1i orri oran mpbi opprc3 mtbi opex elsnc mtbir nex cleqid negb ax-mp
      pm5.21 mp2an biabi dfdm3 dfnul2 3eqtr4 ) ACZBCZDZEFZFZGZBHZAIAAJZKZAIVBLE
      VDVFAVDKVFKZVDVFMVCBVCUTVANZUROGZKZUSOGZKPZVHVIVKQVLKVIVKVKVJBRSTVIVKUAUB
      URUSUCUDUTVAURUSUEUFUGUHVEVGURUIVEUJUKVDVFULUMUNABVBUOAUPUQ $.
      $( [30-Jan-04] $) $( [30-Jan-04] $)

    $( The domain of the identity relation is the universe. $)
    dmi $p |- dom I = V $=
      ( vx vy cv cid wbr wex cab weq cdm cvv a9e visset ideq cleqcom bitr biex
      mpbir cleqid 2th biabi df-dm df-v 3eqtr4 ) ACZBCZDEZBFZAGAAHZAGDIJUGUHAUG
      UHUGBAHZBFBAKUFUIBUFABHUIUDUEALBLMUDUENOPQUDRSTABDUAAUBUC $.
      $( [30-Apr-98] $)

    $( The domain of the universe is the universe. $)
    dmv $p |- dom V = V $=
      ( cvv cdm ssv cid dmi wss dmss ax-mp eqsstr3 eqssi ) ABZAKCADBZKEDAFLKFDC
      DAGHIJ $.
      $( [8-Aug-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The domain of a singleton of an ordered pair is the singleton of the
       first member. $)
    dmsnop $p |- dom { <. A , B >. } = { A } $=
      ( vx vy cvv wcel cop csn cdm wceq cv wex cab wa visset opthg opex elsnc
      syl5bb biexdv 19.42v syl6bb wb isset iba sylbi bitr4d biabdv dfdm3 df-sn
      3eqtr4g wn opprc2 sneq dmeq 3syl c0 anidm opprc3 bitr3 dmeqd dmsnsn0
      syl6eq snprc biimp eqtr4d pm2.61i ) BEFZABGZHZIZAHZJVHCKZDKZGZVJFZDLZCMVM
      AJZCMZVKVLVHVQVRCVHVQVRVNBJZDLZNZVRVHVQVRVTNZDLWBVHVPWCDVHVOVIJWCVPVMVNAB
      ECOZDOZPVOVIVMVNQZRSTVRVTDUAUBVHWAVRWBUCDBUDWAVRUEUFUGUHCDVJUICAUJZUKVHUL
      ZVKAAGZHZIZVLWHVIWIJVJWJJVKWKJABUMVIWIUNVJWJUOUPAEFZWKVLJWLVOWJFZDLZCMVSW
      KVLWLWNVRCWLWNVRVNAJZDLZNZVRWLWNVRWONZDLWQWLWMWRDWLVOWIJWRWMVMVNAAEWDWEPV
      OWIWFRSTVRWODUAUBWLWPVRWQUCDAUDWPVRUEUFUGUHCDWJUIWGUKWLULZWKUQVLWSWKUQHZH
      ZIZUQWSWIWTJZWKXBJWSWSWSNXCWSURAAUSUTXCWJXAWIWTUNVAUFVBVCWSVLUQJAVDVEVFVG
      VCVG $.
      $( [31-Jan-04] $) $( [30-Jan-04] $)
  $}

  $( The domain of the singleton of the singleton of a singleton. $)
  dmsnsnsn $p |- dom { { { A } } } = { A } $=
    ( cop csn cdm cpr wceq dfsn2 preq2 ax-mp df-op 3eqtr4r sneqi dmeqi dmsnop
    eqtr3 ) AABZCZDACZCZCZDRQTPSRREZRAAEZEZSPRUBFUAUCFAGRUBRHIRGAAJKLMAANO $.
    $( [17-Sep-04] $) $( [15-Sep-04] $)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( An empty domain implies an empty range. $)
    dm0rn0 $p |- ( dom A = (/) <-> ran A = (/) ) $=
      ( vx vy cv wbr wex cab c0 wceq cdm crn wcel wb wal wn excom bineg alnex
      3bitr4 noel nbn bial 3bitr3 cleqabr df-dm cleq1i dfrn2 ) BDZCDZAEZCFZBGZH
      IZUJBFZCGZHIZAJZHIAKZHIUKUHHLZMZBNZUNUIHLZMZCNZUMUPUKOZBNZUNOZCNZVAVDUKBF
      ZOUNCFZOVFVHVIVJUJBCPQUKBRUNCRSVEUTBUSUKUHTUAUBVGVCCVBUNUITUAUBUCUKBHUDUN
      CHUDSUQULHBCAUEUFURUOHBCAUGUFS $.
      $( [21-May-98] $)

    $( A relation is empty iff its domain is empty. $)
    reldm0 $p |- ( Rel A -> ( A = (/) <-> dom A = (/) ) ) $=
      ( vx vy wrel c0 wceq cv cop wcel wb wal cdm rel0 cleqrel mpan2 wn eq0
      wex visset eldm2 bineg alnex noel nbn bial bitr3 bitr bitr2 syl6bb ) ADZAE
      FZBGZCGHZAIZUMEIZJZCKZBKZALZEFZUJEDUKURJMBCAENOUTULUSIZPZBKURBUSQVBUQBVBU
      NCRZPZUQVAVCCULABSTUAVDUNPZCKUQUNCUBVEUPCUOUNUMUCUDUEUFUGUEUHUI $.
      $( [?] $) $( [15-Sep-04] $)

    $( The domain of a set is a set.  Corollary 6.8(2) of [TakeutiZaring]
       p. 26. $)
    dmexg $p |- ( A e. B -> dom A e. V ) $=
      ( vx vy wcel cuni cvv cdm uniexg wss cv cop wex cab dfdm3 opeluu pm3.26d
      19.23aiv ss2abi abid2 sseqtr eqsstr ssexg mpi 3syl ) ABEAFZGEUFFZGEZAHZGE
      ZABIUFGIUHUIUGJUJUICKZDKZLAEZDMZCNZUGCDAOUOUKUGEZCNUGUNUPCUMUPDUMUPULUGEC
      DAPQRSCUGTUAUBUIUGGUCUDUE $.
      $( [7-Apr-95] $)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( The domain of a cross product.  Part of Theorem 3.13(x) of [Monk1]
       p. 37. $)
    dmxp $p |- ( -. B = (/) -> dom ( A X. B ) = A ) $=
      ( vy vx c0 wceq wn cv wcel wex wral cxp cdm n0 biimp a1d r19.21aiv wa
      copab dmopab2 df-xp dmeqi cleq1i bitr4 sylib ) BEFGZCHBIZCJZDAKZABLZMZAFZ
      UFUHDAUFUHDHAIZUFUHCBNOPQUIUMUGRDCSZMZAFULUGDCATUKUOAUJUNDCABUAUBUCUDUE
      $.
      $( [28-Jul-95] $)
  $}

  $( The domain of a cross product. $)
  dmxpid $p |- dom ( A X. A ) = A $=
    ( c0 wceq cxp cdm xpeq1 xp0 syl6eq dmeq dm0 syl id eqtr4d dmxp pm2.61i ) AB
    CZAADZEZACPRBAPQBCZRBCPQBADBABAFAGHSRBEBQBIJHKPLMAANO $.
    $( [28-Jul-95] $)

  ${
    $d x y A $.  $d x y B $.
    $( The first member of an ordered pair in a relation belongs to the
       domain of the relation. $)
    elreldm $p |- ( ( Rel A /\ B e. A ) -> |^| |^| B e. dom A ) $=
      ( vx vy wrel wcel cint cdm cv cop wceq wex wi cvv cxp wss df-rel ssel
      sylbi elvv syl6ib eleq1 visset opeldm syl6bi inteq inteqd op1st syl6eq
      eleq1d sylibrd 19.23aivv syl6 pm2.43d imp ) AEZBAFZBGZGZAHZFZUPUQVAUPUQBC
      IZDIZJZKZDLCLZUQVAMZUPUQBNNOZFZVFUPAVHPUQVIMAQAVHBRSCDBTUAVEVGCDVEUQVBUTF
      ZVAVEUQVDAFVJBVDAUBVBVCACUCZUDUEVEUSVBUTVEUSVDGZGVBVEURVLBVDUFUGVBVCVKUHU
      IUJUKULUMUNUO $.
      $( [5-Aug-04] $) $( [28-Jul-04] $)
  $}

  $( Equality theorem for range. $)
  rneq $p |- ( A = B -> ran A = ran B ) $=
    ( wceq ccnv cdm crn cnveq dmeqd df-rn 3eqtr4g ) ABCZADZEBDZEAFBFKLMABGHAIBI
    J $.
    $( [29-Dec-96] $)

  ${
    rneqi.1 $e |- A = B $.
    $( Equality inference for range. $)
    rneqi $p |- ran A = ran B $=
      ( wceq crn rneq ax-mp ) ABDAEBEDCABFG $.
      $( [5-Mar-04] $) $( [4-Mar-04] $)
  $}

  ${
    rneqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for range. $)
    rneqd $p |- ( ph -> ran A = ran B ) $=
      ( wceq crn rneq syl ) ABCEBFCFEDBCGH $.
      $( [5-Mar-04] $) $( [4-Mar-04] $)
  $}

  $( Subset theorem for range. $)
  rnss $p |- ( A (_ B -> ran A (_ ran B ) $=
    ( wss ccnv cdm crn cnvss dmss syl df-rn 3sstr4g ) ABCZADZEZBDZEZAFBFLMOCNPC
    ABGMOHIAJBJK $.
    $( [22-Mar-98] $)

  ${
    brelrn.1 $e |- A e. V $.
    brelrn.2 $e |- B e. V $.
    $( The second argument of a binary relation belongs to its range. $)
    brelrn $p |- ( A C B -> B e. ran C ) $=
      ( wbr ccnv cdm wcel crn brcnv breldm sylbir df-rn eleq2i sylibr ) ABCFZBC
      GZHZIZBCJZIQBARFTBACEDKBARELMUASBCNOP $.
      $( [15-Aug-04] $) $( [13-Aug-04] $)

    $( Membership of second member of an ordered pair in a range. $)
    opelrn $p |- ( <. A , B >. e. C -> B e. ran C ) $=
      ( cop wcel wbr crn df-br brelrn sylbir ) ABFCGABCHBCIGABCJABCDEKL $.
      $( [23-Feb-97] $)
  $}

  ${
    $d x y z w v $.  $d z w v A $.  $( ` w v ` are dummy. $)
    dfrnf.1 $e |- ( z e. A -> A. x z e. A ) $.
    dfrnf.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Range definition requiring only that ` x ` and ` y ` not be
       'free' in ` A ` (but not necessarily absent from it). $)
    dfrnf $p |- ran A = { y | E. x <. x , y >. e. A } $=
      ( vv vw crn cv cop wcel wex cab dfrn3 ax-17 hbel weq opeq1 eleq1d cbvex
      biabi hbex opeq2 biexdv cbvab 3eqtr ) DIGJZHJZKZDLZGMZHNAJZUIKZDLZAMZHNUM
      BJZKZDLZAMZBNGHDOULUPHUKUOGAACCUJDCJZUJLAPEQUOGPGARUJUNDUHUMUISTUAUBUPUTH
      BUOBABCCUNDVAUNLBPFQUCUTHPHBRZUOUSAVBUNURDUIUQUMUDTUEUFUG $.
      $( [14-Aug-95] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` y ` is dummy. $)
    elrn.1 $e |- A e. V $.
    $( Membership in a range. $)
    elrn $p |- ( A e. ran B <-> E. x <. x , A >. e. B ) $=
      ( vy cv cop wcel wex crn wceq opeq2 eleq1d biexdv dfrn3 elab2 ) AFZEFZGZC
      HZAIQBGZCHZAIEBCJDRBKZTUBAUCSUACRBQLMNAECOP $.
      $( [10-Jul-94] $)

    $( Membership in a range. $)
    elrn2 $p |- ( A e. ran B <-> E. x x B A ) $=
      ( crn wcel cv cop wex wbr elrn df-br biex bitr4 ) BCEFAGZBHCFZAIOBCJZAIAB
      CDKQPAOBCLMN $.
      $( [5-Apr-04] $) $( [2-Apr-04] $)
  $}

  ${
    $d x y z w $.  $d y z w A $.
    hbrn.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for range. $)
    hbrn $p |- ( y e. ran A -> A. x y e. ran A ) $=
      ( vz vw cv cop wcel wex wal crn ax-17 wel hbel hbex visset elrn bial
      3imtr4 ) EGZBGZHZCIZEJZUEAKUBCLIZUFAKUDAEAFEUCCFGUCIAMABBUACBENAMDOOPEUBC
      BQRZUFUEAUGST $.
      $( [1-Sep-99] $)
  $}

  ${
    $d x y z w $.  $d y z w A $.  $( ` z w ` are dummy. $)
    hbdm.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for domain. $)
    hbdm $p |- ( y e. dom A -> A. x y e. dom A ) $=
      ( cv ccnv crn wcel wal cdm hbcnv hbrn dfdm4 eleq2i bial 3imtr4 ) BEZCFZGZ
      HZTAIQCJZHZUBAIABRABCDKLUASQCMNZUBTAUCOP $.
      $( [31-Jan-04] $) $( [30-Jan-04] $)
  $}

  ${
    $d x y z $.  $d z ph $.  $( ` z ` is dummy. $)
    $( The range of a class of ordered pairs. $)
    rnopab $p |- ran { <. x , y >. | ph } = { y | E. x ph } $=
      ( vz copab crn cv cop wcel wex cab hbopab1 hbopab2 dfrnf opabid biex
      biabi eqtr ) ABCEZFBGCGHSIZBJZCKABJZCKBCDSABCDLABCDMNUAUBCTABABCOPQR $.
      $( [14-Aug-95] $)
  $}

  $( The range of the empty set is empty.  Part of Theorem 3.8(v) of [Monk1]
     p. 36. $)
  rn0 $p |- ran (/) = (/) $=
    ( c0 cdm wceq crn dm0 dm0rn0 mpbi ) ABACADACEAFG $.
    $( [4-Jul-94] $)

  ${
    $d x y A $.
    $( A relation is empty iff its range is empty. $)
    relrn0 $p |- ( Rel A -> ( A = (/) <-> ran A = (/) ) ) $=
      ( wrel c0 wceq cdm crn reldm0 dm0rn0 syl6bb ) ABACDAECDAFCDAGAHI $.
      $( [?] $) $( [15-Sep-04] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The range of a set is a set.  Corollary 6.8(3) of [TakeutiZaring] p. 26.
       Similar to Lemma 3D of [Enderton] p. 41. $)
    rnexg $p |- ( A e. B -> ran A e. V ) $=
      ( vx vy wcel cuni cvv crn uniexg wss cv cop wex cab dfrn3 opeluu pm3.27d
      19.23aiv ss2abi abid2 sseqtr eqsstr ssexg mpi 3syl ) ABEAFZGEUFFZGEZAHZGE
      ZABIUFGIUHUIUGJUJUICKZDKZLAEZCMZDNZUGCDAOUOULUGEZDNUGUNUPDUMUPCUMUKUGEUPC
      DAPQRSDUGTUAUBUIUGGUCUDUE $.
      $( [31-Mar-95] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Domain of a composition.  Theorem 21 of [Suppes] p. 63. $)
    dmco $p |- dom ( A o. B ) (_ dom B $=
      ( vx vy vz cv ccom wbr wex cab cdm wa cop wcel df-br visset opelco bitr
      biex excom 19.42v pm3.26bd 19.22i sylbi ss2abi df-dm 3sstr4 ) CFZDFZABGZH
      ZDIZCJUHEFZBHZEIZCJUJKBKULUOCULUNUMUIAHZLZDIZEIZUOULUQEIZDIUSUKUTDUKUHUIM
      UJNUTUHUIUJOEUHUIABCPDPQRSUQDETRURUNEURUNUPDIUNUPDUAUBUCUDUECDUJUFCEBUFUG
      $.
      $( [19-Mar-98] $)
  $}

  $( Range of a composition. $)
  rnco $p |- ran ( A o. B ) (_ ran A $=
    ( ccnv ccom cdm crn dmco df-rn cnvco dmeqi eqtr 3sstr4 ) BCZACZDZEZNEABDZFZ
    AFMNGRQCZEPQHSOABIJKAHL $.
    $( [19-Mar-98] $)

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Domain of a composition. $)
    dmcosseq $p |- ( ran B (_ dom A -> dom ( A o. B ) = dom B ) $=
      ( vz vx vy cv crn wcel cdm wi wal ccom wb wss wceq wbr wex wa hbe1 ax-17
      hbim hbal hba1 19.8a syl4 ancld a4s 19.22d 19.21ai pm3.26 19.22i ax-gen
      jctil albi sylibr visset elrn2 eldm bi2im bial cop eldm2 opelco biex
      excom 19.42v bitr 3bitr bi2bi 3imtr4 dfss2 dfcleq ) CFZBGZHZVMAIZHZJZCKZD
      FZABLZIZHZVTBIZHZMZDKZVNVPNWBWDOVTVMBPZDQZVMEFZAPZEQZJZCKZWHWLRZCQZWHCQZM
      ZDKZVSWGWNWPWQJZDKZWQWPJZDKZRWSWNXCXAWNXBDWMDCWIWLDWHDSWLDTUAUBWNWHWOCWMC
      UCWMWHWOJCWMWHWLWHWIWLWHDUDUEUFUGUHUIWTDWOWHCWHWLUJUKULUMWPWQDUNUOVRWMCVO
      WIVQWLDVMBCUPZUQEVMAXDURUSUTWFWRDWCWPWEWQWCVTWJVAWAHZEQWHWKRZCQZEQZWPEVTW
      ADUPZVBXEXGECVTWJABXIEUPVCVDXHXFEQZCQWPXFECVEXJWOCWHWKEVFVDVGVHCVTBXIURVI
      UTVJCVNVPVKDWBWDVLVJ $.
      $( [28-May-98] $)

    $( Domain of a composition. $)
    dmcoeq $p |- ( dom A = ran B -> dom ( A o. B ) = dom B ) $=
      ( cdm crn wceq wss ccom eqimss2 dmcosseq syl ) ACZBDZELKFABGCBCELKHABIJ
      $.
      $( [19-Mar-98] $)
  $}

  $( Range of a composition. $)
  rncoeq $p |- ( dom A = ran B -> ran ( A o. B ) = ran A ) $=
    ( ccnv cdm crn wceq ccom dmcoeq cleqcom df-rn dfdm4 cleq12i bitr cnvco
    dmeqi eqtr 3imtr4 ) BCZDZACZEZFZRTGZDZTDZFADZBEZFZABGZEZAEZFRTHUHUGUFFUBUFU
    GIUGSUFUABJAKLMUJUDUKUEUJUICZDUDUIJULUCABNOPAJLQ $.
    $( [19-Mar-98] $)

  $( Equality theorem for restrictions. $)
  reseq1 $p |- ( A = B -> ( A |` C ) = ( B |` C ) ) $=
    ( wceq cvv cxp cin cres ineq1 df-res 3eqtr4g ) ABDACEFZGBLGACHBCHABLIACJBCJ
    K $.
    $( [7-Aug-94] $)

  $( Equality theorem for restrictions. $)
  reseq2 $p |- ( A = B -> ( C |` A ) = ( C |` B ) ) $=
    ( wceq cvv cxp cin cres xpeq1 ineq2d df-res 3eqtr4g ) ABDZCAEFZGCBEFZGCAHCB
    HMNOCABEIJCAKCBKL $.
    $( [8-Aug-94] $)

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbres.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbres.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for restriction. $)
    hbres $p |- ( y e. ( A |` B ) -> A. x y e. ( A |` B ) ) $=
      ( cv cvv cxp cin wcel wal cres ax-17 hbxp hbin df-res eleq2i bial 3imtr4
      ) BGZCDHIZJZKZUDALUACDMZKZUFALABCUBEABDHFUAHKANOPUEUCUACDQRZUFUDAUGST $.
      $( [15-Sep-03] $)
  $}

  $( A restriction to the empty set is empty. $)
  res0 $p |- ( A |` (/) ) = (/) $=
    ( c0 cres cvv cxp cin df-res xp0 ineq2i in0 3eqtr ) ABCABDEZFABFBABGLBADHIA
    JK $.
    $( [12-Nov-94] $)

  ${
    opelres.1 $e |- B e. V $.
    $( Ordered pair membership in a restriction.  Exercise 13 of [TakeutiZaring]
       p. 25. $)
    opelres $p |- ( <. A , B >. e. ( C |` D ) <->
                    ( <. A , B >. e. C /\ A e. D ) ) $=
      ( cop cres wcel cvv cxp cin wa df-res eleq2i elin opelxp mpbiranr bilan
      3bitr ) ABFZCDGZHTCDIJZKZHTCHZTUBHZLUDADHZLUAUCTCDMNTCUBOUEUFUDUEUFBIHABD
      IEPEQRS $.
      $( [13-Nov-95] $)
  $}

  ${
    opres.1 $e |- B e. V $.
    $( Ordered pair membership in a restriction when the first member belongs
       to the restricting class. $)
    opres $p |- ( A e. D ->
                    ( <. A , B >. e. ( C |` D ) <-> <. A , B >. e. C ) ) $=
      ( wcel cop cres wi opelres pm3.26bd a1i wa biimpr exp com12 impbid ) ADFZ
      ABGZCDHFZSCFZTUAIRTUARABCDEJZKLUARTUARTTUARMUBNOPQ $.
      $( [2-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( A restricted identity relation is equivalent to equality in its
       domain. $)
    resieq $p |- ( ( B e. A /\ C e. A ) -> ( B ( I |` A ) C <-> B = C ) ) $=
      ( vx wcel cid cres wbr wceq wb cv wi breq2 cleq2 bi2bid bilimd cop
      visset opres cvv ideqg mpan2 df-br syl5bbr bitrd syl5bb vtoclg com12 imp
      ) BAEZCAEZBCFAGZHZBCIZJZUKUJUOUJBDKZULHZBUPIZJZLUJUOLDCAUPCIZUSUOUJUTUQUM
      URUNUPCBULMUPCBNOPUJBUPQZULEZURUQUJVBVAFEZURBUPFADRZSUJBUPFHZURVCUJUPTEVE
      URJVDBUPATUAUBBUPFUCUDUEBUPULUCUFUGUHUI $.
      $( [14-May-04] $) $( [30-Apr-04] $)
  $}

  $( Distributive law for restriction over union.  Theorem 31 of [Suppes]
     p. 65. $)
  resun $p |- ( A |` ( B u. C ) ) = ( ( A |` B ) u. ( A |` C ) ) $=
    ( cun cvv cxp cin cres xpun2 ineq2i indi eqtr df-res uneq12i 3eqtr4 ) ABCDZ
    EFZGZABEFZGZACEFZGZDZAPHABHZACHZDRASUADZGUCQUFABCEIJASUAKLAPMUDTUEUBABMACMN
    O $.
    $( [30-Sep-02] $)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The domain of a restriction.  Exercise 14 of [TakeutiZaring]
       p. 25. $)
    dmres $p |- dom ( A |` B ) = ( B i^i dom A ) $=
      ( vx vy cdm cin cres cv cop wcel wex wa visset opelres biex eldm2 biran
      19.41v bitr4 3bitr4r ineqri incom eqtr3 ) AEZBFABGZEZBUDFCUDBUFCHZDHZIZUE
      JZDKUIAJZUGBJZLZDKZUGUFJUGUDJZULLZUJUMDUGUHABDMNODUGUECMZPUPUKDKZULLUNUOU
      RULDUGAUQPQUKULDRSTUAUDBUBUC $.
      $( [1-Aug-94] $)
  $}

  $( A domain restricted to a subclass equals the subclass. $)
  ssdmres $p |- ( A (_ dom B <-> dom ( B |` A ) = A ) $=
    ( cdm wss cin wceq cres df-ss dmres cleq1i bitr4 ) ABCZDALEZAFBAGCZAFALHNMA
    BAIJK $.
    $( [28-Aug-04] $) $( [2-Mar-97] $)

  $( A domain restricted to a subclass equals the subclass. $)
  ssdmresOLD $p |- ( A (_ dom B <-> A = dom ( B |` A ) ) $=
    ( cdm wss cin wceq cres dfss dmres cleq2i bitr4 ) ABCZDAALEZFABAGCZFALHNMAB
    AIJK $.
    $( [2-Mar-97] $)

  $( The domain of a restriction to a set exists. $)
  dmresexg $p |- ( B e. C -> dom ( A |` B ) e. V ) $=
    ( wcel cdm cin cvv cres inex1g dmres eleq1i sylibr ) BCDBAEZFZGDABHEZGDBMCI
    ONGABJKL $.
    $( [7-Apr-95] $)

  $( A class includes its restriction.  Exercise 15 of [TakeutiZaring]
     p. 25. $)
  resss $p |- ( A |` B ) (_ A $=
    ( cres cvv cxp cin df-res inss1 eqsstr ) ABCABDEZFAABGAJHI $.
    $( [2-Aug-94] $)

  $( Commutative law for restriction. $)
  rescom $p |- ( ( A |` B ) |` C ) = ( ( A |` C ) |` B ) $=
    ( cres cvv cxp cin in23 df-res ineq1i 3eqtr4 ) ABDZCEFZGZACDZBEFZGZLCDOBDAP
    GZMGAMGZPGNQAPMHLRMABIJOSPACIJKLCIOBIK $.
    $( [27-Mar-98] $)

  $( Subclass theorem for restriction. $)
  ssres $p |- ( A (_ B -> ( A |` C ) (_ ( B |` C ) ) $=
    ( wss cvv cxp cin cres ssrin df-res 3sstr4g ) ABDACEFZGBLGACHBCHABLIACJBCJK
    $.
    $( [16-Aug-94] $)

  $( Subclass theorem for restriction. $)
  ssres2 $p |- ( A (_ B -> ( C |` A ) (_ ( C |` B ) ) $=
    ( wss cvv cxp cin cres ssid ssxp mpan2 sslin syl df-res 3sstr4g ) ABDZCAEFZ
    GZCBEFZGZCAHCBHPQSDZRTDPEEDUAEIABEEJKQSCLMCANCBNO $.
    $( [22-Mar-98] $)

  $( A restriction is a relation.  Exercise 12 of [TakeutiZaring] p. 25. $)
  relres $p |- Rel ( A |` B ) $=
    ( cres wrel cvv cxp wss cin df-res inss2 xpss sstri eqsstr df-rel mpbir )
    ABCZDPEEFZGPABEFZHZQABISRQARJBEKLMPNO $.
    $( [2-Aug-94] $)

  $( Absorption law for restriction.  Exercise 17 of [TakeutiZaring] p. 25. $)
  resabs1 $p |- ( B (_ C -> ( ( A |` C ) |` B ) = ( A |` B ) ) $=
    ( wss cvv cxp cin cres wceq sseqin2 xpeq1 sylbi xpindi inidm xpeq2 ax-mp
    eqtr2 syl6eq ineq2d inass syl6reqr df-res ineq1i eqtr 3eqtr4g ) BCDZACEFZGZ
    BEFZGZAUIGZACHZBHZABHUFUKAUGUIGZGUJUFUIUNAUFUICBGZEFZUNUFBUOIUIUPIBCJBUOEKL
    UNUOEEGZFZUPCEBEMUQEIURUPIENUQEUOOPQRSAUGUITUAUMULUIGUJULBUBULUHUIACUBUCUDA
    BUBUE $.
    $( [9-Aug-94] $)

  $( Absorption law for restriction. $)
  resabs2 $p |- ( B (_ C -> ( ( A |` B ) |` C ) = ( A |` B ) ) $=
    ( wss cvv cxp cin cres wceq ssid wa ssxp dfss sylib mpan2 ineq2d inass
    syl6reqr df-res ineq1i eqtr 3eqtr4g ) BCDZABEFZGZCEFZGZUEABHZCHZUHUCUEAUDUF
    GZGUGUCUDUJAUCEEDZUDUJIZEJUCUKKUDUFDULBCEELUDUFMNOPAUDUFQRUIUHUFGUGUHCSUHUE
    UFABSZTUAUMUB $.
    $( [27-Mar-98] $)

  $( Idempotent law for restriction. $)
  residm $p |- ( ( A |` B ) |` B ) = ( A |` B ) $=
    ( wss cres wceq ssid resabs2 ax-mp ) BBCABDZBDIEBFABBGH $.
    $( [27-Mar-98] $)

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Simplification law for restriction. $)
    relssres $p |- ( ( Rel A /\ dom A (_ B ) -> ( A |` B ) = A ) $=
      ( vx vy wrel cdm wss wa cres wceq pm3.26 cv cop wcel wi ssel visset
      opeldm syl5 ancld opelres syl6ibr adantl relssdv resss jctil eqss sylibr
      ) AEZAFZBGZHZABIZAGZAUMGZHUMAJULUOUNULCDAUMUIUKKUKCLZDLZMZANZURUMNZOUIUKU
      SUSUPBNZHUTUKUSVAUKUPUJNVAUSUJBUPPUPUQACQRSTUPUQABDQUAUBUCUDABUEUFUMAUGUH
      $.
      $( [15-Mar-04] $) $( [16-Aug-94] $)
  $}

  $( The restriction of a set is a set. $)
  resexg $p |- ( A e. C -> ( A |` B ) e. V ) $=
    ( wcel cvv cxp cin cres inex1g df-res eleq1i sylibr ) ACDABEFZGZEDABHZEDAMC
    IONEABJKL $.
    $( [28-Mar-98] $)

  ${
    $d x y A $.
    $( Restriction of an abstraction class of ordered pairs. $)
    resopab $p |- ( { <. x , y >. | ph } |` A ) =
                  { <. x , y >. | ( x e. A /\ ph ) } $=
      ( copab cres cvv cxp cin cv wcel wa df-res df-xp visset biantru biopab
      eqtr4 ineq2i incom eqtr inopab 3eqtr ) ABCEZDFUDDGHZIZBJDKZBCEZUDIZUGALBC
      EUDDMUFUDUHIUIUEUHUDUEUGCJGKZLZBCEUHBCDGNUGUKBCUJUGCOPQRSUDUHTUAUGABCUBUC
      $.
      $( [5-Nov-02] $)
  $}

  ${
    $d x y A $.
    $( A subclass of the identity function is the identity function restricted
       to its domain. $)
    iss $p |- ( A (_ I <-> A = ( I |` dom A ) ) $=
      ( vx vy cid wss cdm cres wceq cv cop wcel wb wal weq wa wi ssel wbr
      df-br visset ideq bitr3 syl6ib pm4.71r sylib wex cleqcom biran syl6bb
      biexdv opeq2 eleq1d ceqsexv eldm2 syl5bb biland pm5.32i bitr4d opelres
      bitr2 19.21aivv wrel reli ssrel mpi relres cleqrel mpan2 syl mpbird
      resss sseq1 mpbiri impbi ) ADEZADAFZGZHZVOVRBIZCIZJZAKZWAVQKZLZCMBMZVOWDB
      CVOWBBCNZVSVPKZOZWCVOWBWFWBOZWHVOWBWFPWBWILVOWBWADKZWFADWAQWJVSVTDRWFVSVT
      DSVSVTBTZCTZUAUBZUCWBWFUDUEZVOWHWFVSVSJZAKZOWIVOWGWPWFVOWBCUFZWPWGVOWQCBN
      ZWBOZCUFWPVOWBWSCVOWBWIWSWNWFWRWBVSVTUGUHUIUJWBWPCVSWKWRWAWOAVTVSVSUKULUM
      UICVSAWKUNUOUPWFWPWBWFWOWAAVSVTVSUKULUQUIURWCWJWGOWHVSVTDVPWLUSWJWFWGWMUH
      UTUIVAVOAVBZVRWELZVODVBWTVCADVDVEWTVQVBXADVPVFBCAVQVGVHVIVJVRVOVQDEDVPVKA
      VQDVLVMVN $.
      $( [16-Dec-03] $) $( [13-Dec-03] $)
  $}

  $( The domain of the restricted identity function. $)
  dmresi $p |- dom ( I |` A ) = A $=
    ( cid cdm wss cres wceq cvv ssv dmi sseqtr4 ssdmres mpbi ) ABCZDBAECAFAGMAH
    IJABKL $.
    $( [30-Aug-04] $) $( [27-Aug-04] $)

  $( Any relation restricted to the universe is itself. $)
  resid $p |- ( Rel A -> ( A |` V ) = A ) $=
    ( wrel cdm cvv wss cres wceq ssv relssres mpan2 ) ABACZDEADFAGKHADIJ $.
    $( [17-Mar-04] $) $( [16-Mar-04] $)

  $( Equality theorem for image. $)
  imaeq1 $p |- ( A = B -> ( A " C ) = ( B " C ) ) $=
    ( wceq cres crn cima reseq1 rneqd df-ima 3eqtr4g ) ABDZACEZFBCEZFACGBCGLMNA
    BCHIACJBCJK $.
    $( [14-Aug-94] $)

  $( Equality theorem for image. $)
  imaeq2 $p |- ( A = B -> ( C " A ) = ( C " B ) ) $=
    ( wceq cres crn cima reseq2 rneqd df-ima 3eqtr4g ) ABDZCAEZFCBEZFCAGCBGLMNA
    BCHICAJCBJK $.
    $( [14-Aug-94] $)

  ${
    $d x y A $.  $d x y B $.
    $( Alternate definition of image.  Compare definition (d) of [Enderton]
       p. 44. $)
    dfima2 $p |- ( A " B ) = { y | E. x e. B x A y } $=
      ( cima cres crn cv cop wcel wex cab wbr wrex df-ima dfrn3 wa cvv cxp cin
      df-res eleq2i elin ancom df-br visset biantru opelxp bitr4 bi2an bitr2
      3bitr biex df-rex biabi 3eqtr ) CDECDFZGAHZBHZIZUQJZAKZBLURUSCMZADNZBLCDO
      ABUQPVBVDBVBURDJZVCQZAKVDVAVFAVAUTCDRSZTZJUTCJZUTVGJZQZVFUQVHUTCDUAUBUTCV
      GUCVFVCVEQVKVEVCUDVCVIVEVJURUSCUEVEVEUSRJZQVJVLVEBUFZUGURUSDRVMUHUIUJUKUL
      UMVCADUNUIUOUP $.
      $( [22-Apr-04] $) $( [19-Apr-04] $)

    $( Alternate definition of image.  Compare definition (d) of [Enderton]
       p. 44. $)
    dfima3 $p |- ( A " B ) = { y | E. x ( x e. B /\ <. x , y >. e. A ) } $=
      ( cima cv wbr wrex cab wcel cop wa wex dfima2 df-rex df-br bilan biex
      bitr biabi eqtr ) CDEAFZBFZCGZADHZBIUBDJZUBUCKCJZLZAMZBIABCDNUEUIBUEUFUDL
      ZAMUIUDADOUJUHAUDUGUFUBUCCPQRSTUA $.
      $( [14-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` y ` is dummy. $)
    elima.1 $e |- A e. V $.
    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima $p |- ( A e. ( B " C ) <-> E. x e. C x B A ) $=
      ( vy cv wbr wrex cima wceq breq2 birexdv dfima2 elab2 ) AGZFGZCHZADIPBCHZ
      ADIFBCDJEQBKRSADQBPCLMAFCDNO $.
      $( [22-Apr-04] $) $( [19-Apr-04] $)

    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima2 $p |- ( A e. ( B " C ) <-> E. x ( x e. C /\ x B A ) ) $=
      ( cima wcel cv wbr wrex wa wex elima df-rex bitr ) BCDFGAHZBCIZADJPDGQKAL
      ABCDEMQADNO $.
      $( [12-Aug-04] $) $( [11-Aug-04] $)

    $( Membership in an image.  Theorem 34 of [Suppes] p. 65. $)
    elima3 $p |- ( A e. ( B " C ) <-> E. x ( x e. C /\ <. x , A >. e. B ) ) $=
      ( cima wcel cv wbr wa wex cop elima2 df-br bilan biex bitr ) BCDFGAHZDGZR
      BCIZJZAKSRBLCGZJZAKABCDEMUAUCATUBSRBCNOPQ $.
      $( [14-Aug-94] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d x y z w $.  $( ` z w ` are dummy. $)
    hbima.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbima.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for image. $)
    hbima $p |- ( y e. ( A " B ) -> A. x y e. ( A " B ) ) $=
      ( vz vw cv wcel cop wa wex wal cima wel ax-17 hbel hbop hban hbex visset
      elima3 bial 3imtr4 ) GIZDJZUFBIZKZCJZLZGMZULANUHCDOJZUMANUKAGUGUJAAHBUFDH
      GPAQZFRAHBUICAHUFUHUNHBPAQSERTUAGUHCDBUBUCZUMULAUOUDUE $.
      $( [30-Dec-96] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The image of the domain of a class is the range of the class. $)
    imadmrn $p |- ( A " dom A ) = ran A $=
      ( vx vy cv cdm wcel cop wa wex cab cima crn visset opeldm pm4.71i ancom
      bitr2 biex biabi dfima3 dfrn3 3eqtr4 ) BDZAEZFZUCCDZGAFZHZBIZCJUGBIZCJAUD
      KALUIUJCUHUGBUGUGUEHUHUGUEUCUFABMNOUGUEPQRSBCAUDTBCAUAUB $.
      $( [14-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The image of a class is a subset of its range.  Theorem 3.16(xi) of
       [Monk1] p. 39. $)
    imassrn $p |- ( A " B ) (_ ran A $=
      ( vx vy cv wcel cop wa wex cab cima crn pm3.27 19.22i ss2abi dfima3
      dfrn3 3sstr4 ) CEZBFZSDEGAFZHZCIZDJUACIZDJABKALUCUDDUBUACTUAMNOCDABPCDAQR
      $.
      $( [31-Mar-95] $)
  $}

  $( The image of a set is a set.  Theorem 3.17 of [Monk1] p. 39. $)
  imaexg $p |- ( A e. C -> ( A " B ) e. V ) $=
    ( wcel crn cvv cima rnexg wss imassrn ssexg mpi syl ) ACDAEZFDZABGZFDZACHOP
    NIQABJPNFKLM $.
    $( [24-Jul-95] $)

  ${
    $d x y A $.
    $( Image under the identity relation.  Theorem 3.16(viii) of [Monk1]
       p. 38. $)
    imai $p |- ( I " A ) = A $=
      ( vx vy cid cima cv wcel cop wa wex cab dfima3 weq wbr df-br visset ideq
      bitr3 bilan ancom bitr biex eleq1 ceqsexv biabi abid2 3eqtr ) DAEBFZAGZUH
      CFZHDGZIZBJZCKUJAGZCKABCDALUMUNCUMBCMZUIIZBJUNULUPBULUIUOIUPUKUOUIUKUHUJD
      NUOUHUJDOUHUJBPCPZQRSUIUOTUAUBUIUNBUJUQUHUJAUCUDUAUECAUFUG $.
      $( [30-Apr-98] $)
  $}

  $( The range of the restricted identity function. $)
  rnresi $p |- ran ( I |` A ) = A $=
    ( cid cima cres crn df-ima imai eqtr3 ) BACBADEABAFAGH $.
    $( [28-Aug-04] $) $( [27-Aug-04] $)

  $( Image of the empty set.  Theorem 3.16(ii) of [Monk1] p. 38. $)
  ima0 $p |- ( A " (/) ) = (/) $=
    ( c0 cima cres crn df-ima res0 rneqi rn0 3eqtr ) ABCABDZEBEBABFKBAGHIJ $.
    $( [20-May-98] $)

  ${
    $d y x A $.  $d y x R $.  $( ` x ` is dummy. $)
    $( Image of a singleton. $)
    imasn $p |- ( Rel R -> ( R " { A } ) = { y | <. A , y >. e. R } ) $=
      ( vx wrel cvv wcel csn cima cv cop cab wceq wn wa c0 snprc imaeq2 sylbi
      ima0 syl6eq adantl wex cxp wss wi df-rel ssel opelxpex syl6 con3d imp
      nexdv noel3 bicon1 sylib eqtr4d exp opeq1 eleq1d ceqsexgv elsn biran
      biex syl5bb biabdv dfima3 syl5eq pm2.61d2 ) CEZBFGZCBHZIZBAJZKZCGZALZMZVJ
      VKNZVRVJVSOZVMPVQVSVMPMVJVSVMCPIZPVSVLPMVMWAMBQVLPCRSCTUAUBVTVPAUCZNVQPMZ
      VTVPAVJVSVPNVJVPVKVJVPVOFFUDZGZVKVJCWDUEVPWEUFCUGCWDVOUHSBVNFFUIUJUKULUMW
      CWBVPAUNUOUPUQURVKDJZVLGZWFVNKZCGZOZDUCZALVQVMVKWKVPAVKWFBMZWIOZDUCVPWKWI
      VPDBFWLWHVOCWFBVNUSUTVAWJWMDWGWLWIDBVBVCVDVEVFDACVLVGVHVI $.
      $( [20-May-98] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    elimasn.1 $e |- B e. V $.
    elimasn.2 $e |- C e. V $.
    $( Membership in an image of a singleton. $)
    elimasn $p |- ( C e. ( A " { B } ) <-> <. B , C >. e. A ) $=
      ( vx csn cima wcel cv cop wa wex wceq elima3 elsn biran biex opeq1
      eleq1d ceqsexv 3bitr ) CABGZHIFJZUCIZUDCKZAIZLZFMUDBNZUGLZFMBCKZAIZFCAUCE
      OUHUJFUEUIUGFBPQRUGULFBDUIUFUKAUDBCSTUAUB $.
      $( [16-Mar-04] $) $( [15-Mar-04] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    eliniseg.1 $e |- C e. V $.
    $( Membership in an initial segment.  The idiom ` ( ``' A " { B } ) ` ,
       meaning ` { x | x A B } ` , is used to specify an initial segment in
       (for example) Definition 6.21 of [TakeutiZaring] p. 30. $)
    eliniseg $p |- ( B e. D -> ( C e. ( `' A " { B } ) <-> C A B ) ) $=
      ( vx ccnv cv csn cima wcel wbr wb wceq sneq imaeq2 syl eleq2d breq2
      bi2bid cop visset elimasn df-br brcnv bitr3 bitr vtoclg ) CAGZFHZIZJZKZCU
      JALZMCUIBIZJZKZCBALZMFBDUJBNZUMUQUNURUSULUPCUSUKUONULUPNUJBOUKUOUIPQRUJBC
      ASTUMUJCUAUIKZUNUIUJCFUBZEUCUTUJCUILUNUJCUIUDUJCAVAEUEUFUGUH $.
      $( [15-May-04] $) $( [28-Apr-04] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An idiom that signifies an initial segment of an ordering, used, for
       example, in Definition 6.21 of [TakeutiZaring] p. 30. $)
    iniseg $p |- ( B e. C -> ( `' A " { B } ) = { x | x A B } ) $=
      ( wcel cvv ccnv csn cima cv wbr cab wceq elisset visset eliniseg biabrdv
      syl ) CDECFEZBGCHIZAJZCBKZALMCDNSUBATBCUAFAOPQR $.
      $( [15-May-04] $) $( [28-Apr-04] $)
  $}

  ${
    $d x y z R $.  $d x A $.
    $( Alternate definition of founded relation.  Definition 6.21 of
       [TakeutiZaring] p. 30. $)
    dffr3 $p |- ( R Fr A <-> A. x ( ( x (_ A /\ -. x = (/) ) ->
                E. y e. x ( x i^i ( `' R " { y } ) ) = (/) ) ) $=
      ( vz wfr cv wss c0 wceq wn wa wbr cab cin wrex wi wal ccnv csn cima
      dffr2 cvv wcel visset iniseg ax-mp ineq2i cleq1i birex bilim bial bitr4
      ) CDFAGZCHUNIJKLZUNEGBGZDMENZOZIJZBUNPZQZARUOUNDSUPTUAZOZIJZBUNPZQZARABEC
      DUBVFVAAVEUTUOVDUSBUNVCURIVBUQUNUPUCUDVBUQJBUEEDUPUCUFUGUHUIUJUKULUM $.
      $( [15-May-04] $) $( [23-Apr-04] $)
  $}

  $( Subset theorem for image. $)
  imass1 $p |- ( A (_ B -> ( A " C ) (_ ( B " C ) ) $=
    ( wss cres crn cima ssres rnss syl df-ima 3sstr4g ) ABDZACEZFZBCEZFZACGBCGM
    NPDOQDABCHNPIJACKBCKL $.
    $( [17-Mar-04] $) $( [16-Mar-04] $)

  $( Subset theorem for image.  Exercise 22(a) of [Enderton] p. 53. $)
  imass2 $p |- ( A (_ B -> ( C " A ) (_ ( C " B ) ) $=
    ( wss cres crn cima ssres2 rnss syl df-ima 3sstr4g ) ABDZCAEZFZCBEZFZCAGCBG
    MNPDOQDABCHNPIJCAKCBKL $.
    $( [22-Mar-98] $)

  $( The image of a singleton outside the domain is empty. $)
  ndmima $p |- ( -. A e. dom B -> ( B " { A } ) = (/) ) $=
    ( cdm wcel wn csn cres crn c0 cima wceq cin disjsn biimpr dmres incom eqtr
    syl5eq dm0rn0 sylib df-ima ) ABCZDEZBAFZGZHZIBUDJUCUECZIKUFIKUCUBUDLZIUGUHI
    KUCUBAMNUGUDUBLUHBUDOUDUBPQRUESTBUDUAR $.
    $( [22-May-98] $)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( A converse is a relation.  Theorem 12 of [Suppes] p. 62. $)
    relcnv $p |- Rel `' A $=
      ( vy vx ccnv wrel cv wbr copab relopab wceq wb df-cnv releq ax-mp mpbir
      ) ADZEZBFCFAGZCBHZEZRCBIPSJQTKCBALPSMNO $.
      $( [29-Oct-96] $)
  $}

  ${
    $d x y z R $.
    $( Two ways of saying a relation is transitive.  Definition of
       transitivity in [Schechter] p. 51. $)
    cotr $p |- ( ( R o. R ) (_ R <->
             A. x A. y A. z ( ( x R y /\ y R z ) -> x R z ) ) $=
      ( ccom wss cv wbr wa wi wal wex copab cop wcel ssel df-br syl6ibr opabid
      syl5ibr df-co sseq1i 19.23v 3imtr4 19.21aiv alcom sylibr ssopab2 bial
      bitr bitr4 opabss sstr mpi sylbir syl5ss impbi ) DDEZDFZAGZBGZDHVACGZDHIZ
      UTVBDHZJZCKBKZAKZUSVFAUSVEBKZCKZVFUSVHCVCBLZACMZDFZVJVDJZUSVHVLUTVBNZVKOZ
      VDVJVLVOVNDOVDVKDVNPUTVBDQRVJACSTURVKDACBDDUAZUBVCVDBUCZUDUEVEBCUFZUGUEVG
      VKDURVGVKVDACMZFZVLVTVMCKZAKVGVJVDACUHVFWAAVFVIWAVRVHVMCVQUIUJUIUKVTVSDFV
      LACDULVKVSDUMUNUOVPUPUQ $.
      $( [27-Dec-96] $)
  $}

  ${
    $d x y R $.
    $( Two ways of saying a relation is symmetric.  Similar to definition of
       symmetry in [Schechter] p. 51. $)
    cnvsym $p |- ( `' R (_ R <-> A. x A. y ( x R y -> y R x ) ) $=
      ( ccnv wss cv wbr wi wal copab df-cnv sseq1i cop wcel ssel df-br syl6ibr
      opabid syl5ibr sylbi 19.21aivv ssopab2 alcom bitr opabss sstr mpi sylbir
      syl5ss impbi ) CDZCEZAFZBFZCGZUNUMCGZHZBIAIZULUQABULUOBAJZCEZUQUKUSCBACKZ
      LUTUNUMMZUSNZUPUOUTVCVBCNUPUSCVBOUNUMCPQUOBARSTUAURUSCUKURUSUPBAJZEZUTVEU
      QAIBIURUOUPBAUBUQBAUCUDVEVDCEUTBACUEUSVDCUFUGUHVAUIUJ $.
      $( [28-Dec-96] $)

    $( Two ways of saying a relation is antisymmetric.  Definition of
       antisymmetry in [Schechter] p. 51. $)
    intasym $p |- ( ( R i^i `' R ) (_ I <->
                  A. x A. y ( ( x R y /\ y R x ) -> x = y ) ) $=
      ( ccnv cin cid wss cv cop wcel wi wal wbr wa weq wrel wb inss2 relcnv
      ssrel mp2 relss ax-mp df-br visset brcnv bitr3 bi2an elin bitr4 ideq
      bi2im bi2al ) CCDZEZFGZAHZBHZIZUOJZUSFJZKZBLALZUQURCMZURUQCMZNZABOZKZBLAL
      UOPZUPVCQUOUNGUNPVICUNRCSUOUNTUAABUOFUBUCVHVBABVFUTVGVAVFUSCJZUSUNJZNUTVD
      VJVEVKUQURCUDVEUQURUNMVKUQURCAUEZBUEZUFUQURUNUDUGUHUSCUNUIUJVGUQURFMVAUQU
      RVLVMUKUQURFUDUGULUMUJ $.
      $( [?] $) $( [9-Sep-04] $)

    $( Two ways of saying a relation is irreflexive.  Definition of
       irreflexivity in [Schechter] p. 51. $)
    intirr $p |- ( ( R i^i I ) = (/) <-> A. x -. x R x ) $=
      ( vy cid cin c0 wceq cv cop wcel wb wal wbr wn wrel wss inss2 reli ssrel
      mp2 rel0 cleqrel mp2an weq wa wex df-br visset opeq2 eleq1d ceqsexv
      bitr4 noel nbn bicon1 ideq cleqcom 3bitr3r bilan ancom elin 3bitr4r
      bitr2 biex exnal 3bitr bicon2 bial bitr ) BDEZFGZAHZCHZIZVJJZVNFJZKZCLZAL
      ZVLVLBMZNZALVJOZFOVKVSKVJDPDOWBBDQRVJDSTUAACVJFUBUCVRWAAVTVRVTCAUDZVNBJZU
      EZCUFZVQNZCUFVRNVTVLVLIZBJZWFVLVLBUGWDWICVLAUHZWCVNWHBVMVLVLUIUJUKULWEWGC
      WGVOWEVOVQVPVOVNUMUNUOWDWCUEWDVNDJZUEWEVOWCWKWDVLVMDMACUDWKWCVLVMWJCUHUPV
      LVMDUGVLVMUQURUSWCWDUTVNBDVAVBVCVDVQCVEVFVGVHVI $.
      $( [?] $) $( [9-Sep-04] $)
  $}

  ${
    soi.1 $e |- A e. V $.
    soi.2 $e |- R Or S $.
    soi.3 $e |- R (_ ( S X. S ) $.
    $( A strict order relation is irreflexive. $)
    soirri $p |- -. A R A $=
      ( wcel wa wbr wn wor sonr mpan adantl brel con3i pm2.61i ) ACGZRHZAABIZJZ
      RUARCBKRUAECABLMNTSAACCBDFOPQ $.
      $( [10-Feb-96] $)

    ${
      sotri.4 $e |- B e. V $.
      sotri.5 $e |- C e. V $.
      $( A strict order relation is a transitive relation. $)
      sotri $p |- ( ( A R B /\ B R C ) -> A R C ) $=
        ( wcel w3a wbr wa wi id 3exp a1d imp43 brel syl2an wor sotr mpan mpcom
        ) AEKZBEKZCEKZLZABDMZBCDMZNZACDMZUFUGNUGUHNUIUJUKUFUGUGUHUIUFUGUHUIOOUG
        UFUGUHUIUIPQRSABEEDIHTBCEEDJHTUAEDUBUIULUMOGEABCDUCUDUE $.
        $( [10-Feb-96] $)
    $}

    ${
      son2lpi.4 $e |- B e. V $.
      $( A strict order relation has no 2-cycle loops. $)
      son2lpi $p |- -. ( A R B /\ B R A ) $=
        ( wbr wa soirri sotri mto ) ABCIBACIJAACIACDEFGKABACDEFGHELM $.
        $( [10-Feb-96] $)
    $}
  $}

  ${
    $d x y z w $.  $d z w ph $.
    $( The converse of an abstraction class of ordered pairs. $)
    cnvopab $p |- `' { <. x , y >. | ph } = { <. y , x >. | ph } $=
      ( vw vz copab ccnv relcnv relopab cv cop wcel visset opelcnv wb ax-17
      hbopab1 hbel hbopab2 hbbi weq opeq1 eleq1d opeq2 bi2bid opabid bitr4
      chv2 bitr cleqreli ) DEABCFZGZACBFZUKHACBIDJZEJZKZULLUOUNKZUKLZUPUMLZUNUO
      UKDMEMNBJZUNKZUKLZUNUTKZUMLZOZURUSOBEURUSBBCEUQUKCJZUQLBPABCEQRBCEUPUMVFU
      PLBPACBESRTBEUAZVBURVDUSVGVAUQUKUTUOUNUBUCVGVCUPUMUTUOUNUDUCUEUTVFKZUKLZV
      FUTKZUMLZOVECDVBVDCCEEVAUKUOVALCPABCESRCEEVCUMUOVCLCPACBEQRTCDUAZVIVBVKVD
      VLVHVAUKVFUNUTUDUCVLVJVCUMVFUNUTUBUCUEVIAVKABCUFACBUFUGUHUHUIUJ $.
      $( [18-Dec-03] $) $( [11-Dec-03] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( The converse of the empty set. $)
    cnv0 $p |- `' (/) = (/) $=
      ( vx vy c0 ccnv relcnv rel0 cv cop wcel visset opelcnv wn wb noel pm5.21
      mp2an bitr4 cleqreli ) ABCDZCCEFAGZBGZHZSIUATHZCIZUBCIZTUACAJBJKUELUDLUEU
      DMUBNUCNUEUDOPQR $.
      $( [6-Apr-98] $)

    $( The converse of the identity relation.  Theorem 3.7(ii) of [Monk1]
       p. 36. $)
    cnvi $p |- `' I = I $=
      ( vx vy cid ccnv relcnv reli weq cv cop wcel cleqcom wbr df-br visset
      ideq bitr3 brcnv 3bitr3 3bitr4r cleqreli ) ABCDZCCEFABGZBAGZAHZBHZIZCJZUF
      UAJZUDUEKUGUDUECLUBUDUECMUDUEANZBNZOPUDUEUALUEUDCLUHUCUDUECUIUJQUDUEUAMUE
      UDUJUIORST $.
      $( [26-Apr-98] $)
  $}

  ${
    op1sta.1 $e |- A e. V $.
    $( Extract the first member of an ordered pair.    (See ~ op2nda to extract
       the second member and ~ op1st for an alternate version.)  (Contributed
       by Raph Levien, 4-Dec-03.) $)
    op1sta $p |- U. dom { <. A , B >. } = A $=
      ( cop csn cdm cuni dmsnop unieqi unisn eqtr ) ABDEFZGAEZGALMABHIACJK $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    cnvsn.1 $e |- A e. V $.
    cnvsn.2 $e |- B e. V $.
    $( Converse of a singleton of an ordered pair. $)
    cnvsn $p |- `' { <. A , B >. } = { <. B , A >. } $=
      ( vy vx cop csn ccnv relcnv relsn cv wceq wa wcel ancom opex elsnc
      visset opth bitr opelcnv 3bitr 3bitr4r cleqreli ) EFABGZHZIZBAGZHZUGJBADK
      ELZBMZFLZAMZNZUNULNZUKUMGZUJOZUQUHOZULUNPURUQUIMUOUQUIUKUMQRUKUMBAESZFSZC
      TUAUSUMUKGZUGOVBUFMUPUKUMUGUTVAUBVBUFUMUKQRUMUKABVAUTDTUCUDUE $.
      $( [11-May-98] $)

    $( The range of a singleton of an ordered pair is the singleton of the
       second member. $)
    rnsnop $p |- ran { <. A , B >. } = { B } $=
      ( cop csn crn ccnv cdm df-rn cnvsn dmeqi dmsnop eqtr ) ABEFZGOHZIZBFZOJQBAE
      FZIRPSABCDKLBAMNN $.
      $( [27-Jul-04] $) $( [24-Jul-04] $)

    $( Extract the second member of an ordered pair.  Theorem 5.12(ii) of
       [Monk1] p. 52.  (See ~ op1st to extract the first member and ~ op2nda
       for an alternate version.) $)
    op2nd $p |- |^| |^| |^| `' { <. A , B >. } = B $=
      ( cop csn ccnv cint cnvsn inteqi opex intsn eqtr op1st ) ABEFGZHZHZHBAEZH
      ZHBQSPRPRFZHROTABCDIJRBAKLMJJBADNM $.
      $( [1-Dec-03] $) $( [25-Nov-03] $)

    $( Extract the second member of an ordered pair.  (See ~ op1sta to extract
       the first member and ~ op2nd for an alternate version.) $)
    op2nda $p |- U. ran { <. A , B >. } = B $=
      ( cop csn crn cuni cdm ccnv df-rn cnvsn dmeqi eqtr unieqi op1sta ) ABEFZG
      ZHBAEFZIZHBRTRQJZITQKUASABCDLMNOBADPN $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)
  $}

  ${
    $d x y A $. $d x y B $. $d x y C $.
    $( Membership in a cross product.  This version requires no quantifiers or
       dummy variables.  See also ~ elxp5 . $)
    elxp4 $p |- ( A e. ( B X. C ) <-> ( A = <. U. dom { A } , U. ran { A } >.
                 /\ ( U. dom { A } e. B /\ U. ran { A } e. C ) ) ) $=
      ( vx vy cxp wcel cv cop wceq wa wex csn cdm cuni crn elxp sneq rneqd
      unieqd visset op2nda syl6req pm4.71ri biran anass bitr biex cvv snex
      rnexg ax-mp uniex opeq2 cleq2d eleq1 biland bi2and ceqsexv dmeqd op1sta
      3bitr dmexg opeq1 birand ) ABCFGADHZEHZIZJZVFBGZVGCGZKZKZELZDLVFAMZNZOZJZ
      AVFVOPZOZIZJZVJVTCGZKZKZKZDLAVQVTIZJZVQBGZWCKZKZDEABCQVNWFDVNWEVRWBKZWDKW
      FVNVGVTJZVMKZELWEVMWNEVMWMVIKZVLKWNVIWOVLVIWMVIVTVHMZPZOVGVIVSWQVIVOWPAVH
      RSTVFVGDUAZEUAUBUCUDUEWMVIVLUFUGUHVMWEEVTVSVOUIGZVSUIGAUJZVOUIUKULUMWMVIW
      BVLWDWMVHWAAVGVTVFUNUOWMVKWCVJVGVTCUPUQURUSUGWBWLWDWBVRWBVQWAMZNZOVFWBVPX
      BWBVOXAAWARUTTVFVTWRVAUCUDUEVRWBWDUFVBUHWEWKDVQVPWSVPUIGWTVOUIVCULUMVRWBW
      HWDWJVRWAWGAVFVQVTVDUOVRVJWIWCVFVQBUPVEURUSVB $.
      $( [23-Feb-04] $) $( [17-Feb-04] $)

    $( Membership in a cross product requiring no quantifiers or dummy
       variables.  Provides a slightly shorter version of ~ elxp4 when
       the double intersection does not cause proper class problems. $)
    elxp5 $p |- ( A e. ( B X. C ) <-> ( A = <. |^| |^| A , U. ran { A } >.
                 /\ ( |^| |^| A e. B /\ U. ran { A } e. C ) ) ) $=
      ( vx vy cxp wcel cv cop wceq wa wex cint csn crn cuni elxp sneq rneqd
      unieqd visset op2nda syl6req pm4.71ri biran anass bitr biex cvv snex
      rnexg ax-mp uniex opeq2 cleq2d eleq1 biland bi2and ceqsexv inteq inteqd
      op1st 3bitr mpbii adantr 19.23aiv elisset adantl opeq1 birand ceqsexgv
      pm5.21nii ) ABCFGADHZEHZIZJZVMBGZVNCGZKZKZELZDLVMAMZMZJZAVMANZOZPZIZJZVQW
      GCGZKZKZKZDLZAWCWGIZJZWCBGZWJKZKZDEABCQWAWMDWAWLWDWIKZWKKWMWAVNWGJZVTKZEL
      WLVTXBEVTXAVPKZVSKXBVPXCVSVPXAVPWGVONZOZPVNVPWFXEVPWEXDAVORSTVMVNDUAZEUAU
      BUCUDUEXAVPVSUFUGUHVTWLEWGWFWEUIGWFUIGAUJWEUIUKULUMXAVPWIVSWKXAVOWHAVNWGV
      MUNUOXAVRWJVQVNWGCUPUQURUSUGWIWTWKWIWDWIWCWHMZMVMWIWBXGAWHUTVAVMWGXFVBUCU
      DUEWDWIWKUFVCUHWNWCUIGZWSWMXHDWDXHWLWDVMUIGXHXFVMWCUIUPVDVEVFWRXHWPWQXHWJ
      WCBVGVEVHWLWSDWCUIWDWIWPWKWRWDWHWOAVMWCWGVIUOWDVQWQWJVMWCBUPVJURVKVLVC $.
      $( [6-Aug-04] $) $( [1-Aug-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The converse of a union is the union of converses.  Theorem 16 of
       [Suppes] p. 62. $)
    cnvun $p |- `' ( A u. B ) = ( `' A u. `' B ) $=
      ( vx vy cun ccnv relcnv wrel wa pm3.2i relun mpbir cv cop wcel wo elun
      visset opelcnv bi2or bitr4 3bitr4 cleqreli ) CDABEZFZAFZBFZEZUDGUHHUFHZUG
      HZIUIUJAGBGJUFUGKLDMZCMZNZUDOZULUKNZUFOZUOUGOZPZUOUEOUOUHOUNUMAOZUMBOZPUR
      UMABQUPUSUQUTULUKACRZDRZSULUKBVAVBSTUAULUKUDVAVBSUOUFUGQUBUC $.
      $( [25-Mar-98] $)

    $( Distributive law for converse over intersection.  Theorem 15 of [Suppes]
       p. 62. $)
    cnvin $p |- `' ( A i^i B ) = ( `' A i^i `' B ) $=
      ( vx vy cin ccnv relcnv wrel relin ax-mp cv cop wcel wa elin visset
      opelcnv bi2an bitr4 3bitr4 cleqreli ) CDABEZFZAFZBFZEZUBGUDHUFHAGUDUEIJDK
      ZCKZLZUBMZUHUGLZUDMZUKUEMZNZUKUCMUKUFMUJUIAMZUIBMZNUNUIABOULUOUMUPUHUGACP
      ZDPZQUHUGBUQURQRSUHUGUBUQURQUKUDUEOTUA $.
      $( [25-Mar-98] $)
  $}

  $( Distributive law for range over union.  Theorem 8 of [Suppes] p. 60. $)
  rnun $p |- ran ( A u. B ) = ( ran A u. ran B ) $=
    ( cun ccnv cdm crn cnvun dmeqi dmun eqtr df-rn uneq12i 3eqtr4 ) ABCZDZEZADZ
    EZBDZEZCZNFAFZBFZCPQSCZEUAOUDABGHQSIJNKUBRUCTAKBKLM $.
    $( [24-Mar-98] $)

  $( The range of an intersection belongs the intersection of ranges.  Theorem
     9 of [Suppes] p. 60. $)
  rnin $p |- ran ( A i^i B ) (_ ( ran A i^i ran B ) $=
    ( cin ccnv cdm crn cnvin dmeqi dmin eqsstr df-rn ineq12i 3sstr4 ) ABCZDZEZA
    DZEZBDZEZCZNFAFZBFZCPQSCZEUAOUDABGHQSIJNKUBRUCTAKBKLM $.
    $( [?] $) $( [15-Sep-04] $)

  ${
    $d x y z A $.
    $( The range of a union.  Part of Exercise 8 of [Enderton] p. 41. $)
    rnuni $p |- ran U. A = U. x e. A ran x $=
      ( vz vy cuni crn cv ciun cop wcel wex wrex wa eluni biex excom ancom
      19.41v visset elrn bilan 3bitr4 3bitr df-rex bitr4 eliun cleqri ) CBEZFZA
      BAGZFZHZDGCGZIZUHJZDKZUMUKJZABLZUMUIJUMULJUPUJBJZUQMZAKZURUPUNUJJZUSMZAKZ
      DKVCDKZAKVAUOVDDAUNBNOVCDAPVEUTAVBDKZUSMUSVFMVEUTVFUSQVBUSDRUQVFUSDUMUJCS
      ZTUAUBOUCUQABUDUEDUMUHVGTAUMBUKUFUBUG $.
      $( [18-Mar-04] $) $( [17-Mar-04] $)
  $}

  $( Distributive law for image over union.  Theorem 35 of [Suppes] p. 65. $)
  imaun $p |- ( A " ( B u. C ) ) = ( ( A " B ) u. ( A " C ) ) $=
    ( cun cres crn cima resun rneqi rnun eqtr df-ima uneq12i 3eqtr4 ) ABCDZEZFZ
    ABEZFZACEZFZDZAOGABGZACGZDQRTDZFUBPUEABCHIRTJKAOLUCSUDUAABLACLMN $.
    $( [30-Sep-02] $)


  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( An upper bound for intersection with a domain.  Theorem 40 of [Suppes]
       p. 66, who calls it "somewhat surprising." $)
    dminss $p |- ( dom R i^i A ) (_ ( `' R " ( R " A ) ) $=
      ( vx vy cdm cin ccnv cima cv wbr wcel wa wex 19.8a ancoms visset elima2
      sylibr pm3.26 brcnv jca 19.22i eldm biran elin 19.41v 3bitr4 3imtr4
      ssriv ) CBEZAFZBGZBAHZHZCIZDIZBJZUOAKZLZDMZUPUMKZUPUOULJZLZDMUOUKKZUOUNKU
      SVCDUSVAVBUSURUQLZCMZVAURUQVFVECNOCUPBADPZQRUSUQVBUQURSUPUOBVGCPZTRUAUBUO
      UJKZURLUQDMZURLVDUTVIVJURDUOBVHUCUDUOUJAUEUQURDUFUGDUOULUMVHQUHUI $.
      $( [26-Aug-04] $) $( [11-Aug-04] $)

    $( An upper bound for intersection with an image.  Theorem 41 of [Suppes]
       p. 66. $)
    imainss $p |- ( ( R " A ) i^i B ) (_ ( R " ( A i^i ( `' R " B ) ) ) $=
      ( vy vx cima cin ccnv cv wcel wbr wa wex 19.8a visset brcnv sylan5br
      ancoms imlan pm3.26 adantl jca anassrs elin elima2 bilan bitr biran
      sylibr 19.22i 19.41v 3bitr4 3imtr4 ssriv ) DCAFZBGZCACHZBFZGZFZEIZAJZVADI
      ZCKZLZVCBJZLZEMZVAUSJZVDLZEMVCUPJZVCUTJVGVJEVGVBVFVCVAUQKZLZDMZLZVDLZVJVB
      VDVFVPVBVDVFLZLVOVDVQVNVBVFVDVNVFVLVNVDVMDNVCVACDOZEOZPQRSVQVDVBVDVFTUAUB
      UCVIVOVDVIVBVAURJZLVOVAAURUDVTVNVBDVAUQBVSUEUFUGUHUIUJVCUOJZVFLVEEMZVFLVK
      VHWAWBVFEVCCAVRUEUHVCUOBUDVEVFEUKULEVCCUSVRUEUMUN $.
      $( [9-Sep-04] $) $( [11-Aug-04] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The image of a union is the union of the images.  Theorem 3K(a) of
       [Enderton] p. 50. $)
    imaiun $p |- ( A " U. B ) = U. x e. B ( A " x ) $=
      ( vy vz cuni cima cv ciun wcel cop wa wex wrex wel eluni biran biex
      19.41v anass an12 bitr bitr3 excom exdistr 3bitr visset elima3 birex
      df-rex bitr2 eliun 3bitr4 cleqri ) DBCFZGZACBAHZGZIZEHZUOJZUTDHZKBJZLZEMZ
      VBURJZACNZVBUPJVBUSJVEEAOZUQCJZLZAMZVCLZEMZVIVHVCLZEMZLAMZVGVDVLEVAVKVCAU
      TCPQRVMVIVNLZAMZEMVQEMAMVPVLVREVLVJVCLZAMVRVJVCASVSVQAVSVHVIVCLLVQVHVIVCT
      VHVIVCUAUBRUCRVQEAUDVIVNAEUEUFVGVOACNVPVFVOACEVBBUQDUGZUHUIVOACUJUKUFEVBB
      UOVTUHAVBCURULUMUN $.
      $( [1-Sep-04] $) $( [9-Aug-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The converse of a cross product.  Exercise 11 of [Suppes] p. 67. $)
    cnvxp $p |- `' ( A X. B ) = ( B X. A ) $=
      ( vx vy cxp ccnv relcnv relxp cv cop wcel visset opelcnv wa ancom opelxp
      3bitr4 bitr cleqreli ) CDABEZFZBAEZTGBAHCIZDIZJZUAKUDUCJTKZUEUBKZUCUDTCLZ
      DLZMUDAKZUCBKZNUKUJNUFUGUJUKOUDUCABUHPUCUDBAUIPQRS $.
      $( [14-Aug-99] $)
  $}

  $( The cross product with the empty set is empty.  Part of Theorem 3.13(ii)
     of [Monk1] p. 37. $)
  xp0r $p |- ( A X. (/) ) = (/) $=
    ( c0 cxp ccnv wceq xp0 cnveq ax-mp cnvxp cnv0 3eqtr3 ) BACZDZBDZABCBLBEMNEA
    FLBGHBAIJK $.
    $( [17-Apr-04] $) $( [12-Apr-04] $)

  $( Cross products with disjoint sets are disjoint. $)
  xpdisj1 $p |- ( ( A i^i B ) = (/) -> ( ( A X. C ) i^i ( B X. D ) ) = (/) ) $=
    ( cin c0 wceq cxp xpeq1 xp0 syl6eq xpindi syl5eq ) ABEZFGZNCDEZHZFACHBDHEOQ
    FPHFNFPIPJKACBDLM $.
    $( [15-Sep-04] $) $( [13-Sep-04] $)

  $( Cross products with disjoint sets are disjoint. $)
  xpdisj2 $p |- ( ( A i^i B ) = (/) -> ( ( C X. A ) i^i ( D X. B ) ) = (/) ) $=
    ( cin c0 wceq cxp xpeq2 xp0r syl6eq xpindi syl5eq ) ABEZFGZCDEZNHZFCAHDBHEO
    QPFHFNFPIPJKCADBLM $.
    $( [15-Sep-04] $) $( [13-Sep-04] $)

  $( Cross products with two different singletons are disjoint. $)
  xpsndisj $p |- ( -. B = D -> ( ( A X. { B } ) i^i ( C X. { D } ) ) = (/) ) $=
    ( wceq wn csn cin c0 cxp disjsn2 xpdisj2 syl ) BDEFBGZDGZHIEANJCOJHIEBDKNOA
    CLM $.
    $( [29-Jul-04] $) $( [28-Jul-04] $)

  $( The range of a cross product.  Part of Theorem 3.13(x) of [Monk1]
     p. 37. $)
  rnxp $p |- ( -. A = (/) -> ran ( A X. B ) = B ) $=
    ( c0 wceq wn cxp cdm crn dmxp ccnv df-rn cnvxp dmeqi eqtr syl5eq ) ACDEBAFZ
    GZBABFZHZBAISRJZGQRKTPABLMNO $.
    $( [17-Apr-04] $) $( [12-Apr-04] $)

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( A composition is a relation.  Exercise 24 of [TakeutiZaring] p. 25. $)
    relco $p |- Rel ( A o. B ) $=
      ( vx vz vy ccom wrel cv wbr wa wex copab relopab wceq wb df-co releq
      ax-mp mpbir ) ABFZGZCHDHZBIUBEHAIJDKZCELZGZUCCEMTUDNUAUEOCEDABPTUDQRS $.
      $( [26-Jan-97] $)
  $}

  ${
    $d x y R $.
    $( Alternate definition of relation.  Exercise 2 of [TakeutiZaring]
       p. 25. $)
    dfrel2 $p |- ( Rel R <-> `' `' R = R ) $=
      ( vx vy wrel ccnv wceq relcnv wa cv cop wcel wb wal visset opelcnv bitr
      gen2 cleqrel mpbiri mpan releq mpbii impbi ) ADZAEZEZAFZUFDZUDUGUEGZUHUDH
      UGBIZCIZJZUFKZULAKZLZCMBMUOBCUMUKUJJUEKUNUJUKUEBNZCNZOUKUJAUQUPOPQBCUFARS
      TUGUHUDUIUFAUAUBUC $.
      $( [15-Aug-04] $) $( [29-Dec-96] $)
  $}

  ${
    $d x y R $.  $( ` x y ` are dummy. $)
    $( Alternate definition of relation.  Exercise 2 of [TakeutiZaring]
       p. 25. $)
    dfrel2OLD $p |- ( Rel R <-> R = `' `' R ) $=
      ( vx vy wrel ccnv wceq relcnv wa cv cop wcel wb wal visset opelcnv bitr2
      gen2 cleqrel mpbiri mpan2 releq impbi ) ADZAAEZEZFZUCUEDZUFUDGZUCUGHUFBIZ
      CIZJZAKZUKUEKZLZCMBMUNBCUMUJUIJUDKULUIUJUDBNZCNZOUJUIAUPUOOPQBCAUERSTUFUC
      UGUHAUEUASUB $.
      $( [29-Dec-96] $)
  $}

  $( The double converse of a class strips out all elements that are not
     ordered pairs. $)
  cnvcnv $p |- `' `' A = ( A i^i ( V X. V ) ) $=
    ( cvv cxp cin ccnv wceq cnvin cnveq ax-mp wrel wss inss2 df-rel mpbir
    dfrel2 mpbi relcnv relxp sseqtr4 dfss eqtr4 3eqtr3r ) ABBCZDZEZEZAEZUCEZDZE
    ZUDUGEZUEUIFUFUJFAUCGUEUIHIUDJZUFUDFULUDUCKAUCLUDMNUDOPUJUKUHEZDZUKUGUHGUKU
    MKUKUNFUKUCUMUKJUKUCKUGQUKMPUCJUMUCFBBRUCOPSUKUMTPUAUB $.
    $( [25-Dec-03] $) $( [8-Dec-03] $)

  $( The double converse of a class is a subclass.  Exercise 2 of
     [TakeutiZaring] p. 25. $)
  cnvcnvss $p |- `' `' A (_ A $=
    ( ccnv cvv cxp cin cnvcnv inss1 eqsstr ) ABBACCDZEAAFAIGH $.
    $( [28-Jul-04] $) $( [23-Jul-04] $)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Composition with the empty set.  Theorem 20 of [Suppes] p. 63. $)
    co02 $p |- ( A o. (/) ) = (/) $=
      ( vx vy vz c0 ccom relco rel0 cv cop wcel wn wb wbr wa wex noel df-br
      mtbir intnanr nex visset opelco pm5.21 mp2an cleqreli ) BCAEFZEAEGHBIZCIZ
      JZUGKZLUJEKZLUKULMUKUHDIZENZUMUIANZOZDPUPDUNUOUNUHUMJZEKUQQUHUMERSTUADUHU
      IAEBUBCUBUCSUJQUKULUDUEUF $.
      $( [15-May-04] $) $( [24-Apr-04] $)

    $( Composition with the empty set. $)
    co01 $p |- ( (/) o. A ) = (/) $=
      ( c0 ccnv ccom wceq cnv0 coeq2i co02 eqtr2 cnvco 3eqtr4 cnveq ax-mp wrel
      rel0 dfrel2 mpbi relco 3eqtr3r ) BCZCZBADZCZCZBUBTUCEUAUDEBACZTDZTUCUFUEB
      DBTBUEFGUEHIFBAJKTUCLMBNUABEOBPQUBNUDUBEBARUBPQS $.
      $( [15-May-04] $) $( [24-Apr-04] $)

    $( Composition with the identity relation.  Part of Theorem 3.7(i) of
       [Monk1] p. 36. $)
    coi1 $p |- ( Rel A -> ( A o. I ) = A ) $=
      ( vx vy vz cid ccom wrel wceq relco wa cv cop wcel wb wal wbr wex weq
      visset opelco ideq cleqcom bitr biran biex breq1 ceqsexv 3bitr df-br
      gen2 cleqrel mpbiri mpan ) AEFZGZAGZUNAHZAEIUOUPJUQBKZCKZLZUNMZUTAMZNZCOB
      OVCBCVAURUSAPZVBVAURDKZEPZVEUSAPZJZDQDBRZVGJZDQVDDURUSAEBSZCSTVHVJDVFVIVG
      VFBDRVIURVEVKDSUAURVEUBUCUDUEVGVDDURVKVEURUSAUFUGUHURUSAUIUCUJBCUNAUKULUM
      $.
      $( [25-Apr-04] $) $( [22-Apr-04] $)

    $( Composition with the identity relation.  Part of Theorem 3.7(i) of
       [Monk1] p. 36. $)
    coi2 $p |- ( Rel A -> ( I o. A ) = A ) $=
      ( wrel ccnv cid ccom wceq relcnv coi1 ax-mp cnveq cnvco eqtr3 a1i
      dfrel2OLD biimp cnvi cleqcomi coeq2 coeq1 sylan9eq mpan2 sylbi 3eqtr4rd
      ) ABZACZCZDCZUFEZADAEZUFUHFUDUEDEZCZUFUHUJUEFZUKUFFUEBULAGUEHIUJUEJIUEDKL
      MUDAUFFZANZOUDUMUIUHFZUNUMDUGFZUOUGDPQUMUPUIDUFEUHAUFDRDUGUFSTUAUBUC $.
      $( [25-Apr-04] $) $( [22-Apr-04] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.
        $( ` x y z w ` are dummy. $)
    $( Associative law for class composition.  Theorem 27 of [Suppes] p. 64.
       Also Exercise 21 of [Enderton] p. 53.  Interestingly, this law holds for
       any classes whatsoever, not just functions or even relations. $)
    coass $p |- ( ( A o. B ) o. C ) = ( A o. ( B o. C ) ) $=
      ( vx vy vz vw ccom relco cv wbr wa wex cop wcel excom anass bi2ex bitr4
      df-br visset opelco bitr bilan biex 19.42v 3bitr4 biran 19.41v cleqreli
      ) DEABHZCHZABCHZHZUKCIAUMIDJZFJZCKZUPGJZBKZUREJZAKZLZLZGMZFMZUQUSLZVALZFM
      ZGMZUOUTNZULOZVJUNOZVEVCFMGMVIVCFGPVGVCGFUQUSVAQRSUQUPUTUKKZLZFMUQVBGMZLZ
      FMVKVEVNVPFVMVOUQVMUPUTNUKOVOUPUTUKTGUPUTABFUAEUAZUBUCUDUEFUOUTUKCDUAZVQU
      BVDVPFUQVBGUFUEUGUOURUMKZVALZGMVFFMZVALZGMVLVIVTWBGVSWAVAVSUOURNUMOWAUOUR
      UMTFUOURBCVRGUAUBUCUHUEGUOUTAUMVRVQUBVHWBGVFVAFUIUEUGUGUJ $.
      $( [27-Jan-97] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( A relation is included in the cross product of its domain and range.
       Exercise 4.12(t) of [Mendelson] p. 235. $)
    relssdr $p |- ( Rel A -> A (_ ( dom A X. ran A ) ) $=
      ( vx vy wrel cdm crn cxp id cv cop wcel wi wex wa 19.8a jca visset
      opelxp eldm2 elrn bi2an bitr sylibr a1i relssdv ) ADZBCAAEZAFZGZUFHBIZCIZ
      JZAKZULUIKZLUFUMUMCMZUMBMZNZUNUMUOUPUMCOUMBOPUNUJUGKZUKUHKZNUQUJUKUGUHCQZ
      RURUOUSUPCUJABQSBUKAUTTUAUBUCUDUE $.
      $( [3-Aug-94] $)
  $}

  $( The converse of a set is a set.  Corollary 6.8(1) of [TakeutiZaring]
     p. 26. $)
  cnvexg $p |- ( A e. B -> `' A e. V ) $=
    ( wcel ccnv cdm crn cxp wss cvv wrel relcnv relssdr ax-mp wa wi rnexg
    df-rn eleq1i sylib dmexg dfdm4 jca xpexg ssexg 3syl mpi ) ABCZADZUHEZUHFZGZ
    HZUHICZUHJULAKUHLMUGUIICZUJICZNUKICULUMOUGUNUOUGAFZICUNABPUPUIIAQRSUGAEZICU
    OABTUQUJIAUARSUBUIUJIIUCUHUKIUDUEUF $.
    $( [17-Mar-98] $)

  ${
    cnvex.1 $e |- A e. V $.
    $( The converse of a set is a set.  Corollary 6.8(1) of [TakeutiZaring]
       p. 26. $)
    cnvex $p |- `' A e. V $=
      ( cvv wcel ccnv cnvexg ax-mp ) ACDAECDBACFG $.
      $( [20-Dec-03] $) $( [19-Dec-03] $)
  $}

  $( The composition of two sets is a set. $)
  coexg $p |- ( ( A e. C /\ B e. D ) -> ( A o. B ) e. V ) $=
    ( wcel wa ccom cdm crn cxp wss cvv wrel relco relssdr dmco rnco ssxp mp2an
    sstr mpi syl ax-mp wi dmexg rnexg im2an ancoms xpexg ssexg 3syl ) ACEZBDEZF
    ZABGZBHZAIZJZKZUOLEZUOMZUSABNVAUOUOHZUOIZJZKZUSUOOVEVDURKZUSVBUPKVCUQKVFABP
    ABQVBUPVCUQRSUOVDURTUAUBUCUNUPLEZUQLEZFZURLEUSUTUDUMULVIUMVGULVHBDUEACUFUGU
    HUPUQLLUIUOURLUJUKUA $.
    $( [19-Mar-98] $)

  ${
    coex.1 $e |- A e. V $.
    coex.2 $e |- B e. V $.
    $( The composition of two sets is a set. $)
    coex $p |- ( A o. B ) e. V $=
      ( cvv wcel ccom coexg mp2an ) AEFBEFABGEFCDABEEHI $.
      $( [16-Dec-03] $) $( [15-Dec-03] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The domain of a composition.  Exercise 27 of [Enderton] p. 53. $)
    dmco2 $p |- dom ( A o. B ) = ( `' B " dom A ) $=
      ( vx vy vz ccom cdm ccnv cima cv cop wcel wex wa wbr visset opelco biex
      excom 19.42v opelcnv df-br bitr4 df-dm cleqabi bi2an ancom bitr3 bitr
      3bitr eldm2 elima3 3bitr4 cleqri ) CABFZGZBHZAGZIZCJZDJZKUOLZDMZEJZURLZVD
      UTKUQLZNZEMZUTUPLUTUSLVCUTVDBOZVDVAAOZNZEMZDMVKDMZEMVHVBVLDEUTVAABCPZDPQR
      VKDESVMVGEVMVIVJDMZNZVGVIVJDTVPVFVENVGVFVIVEVOVFUTVDKBLVIVDUTBEPVNUAUTVDB
      UBUCVOEUREDAUDUEUFVFVEUGUHUIRUJDUTUOVNUKEUTUQURVNULUMUN $.
      $( [5-Feb-04] $) $( [4-Feb-04] $)
  $}

  ${
    $d x y z A $.
    $( Alternate definition of a function. $)
    dffun2 $p |- ( Fun A <-> ( Rel A /\
                 A. x A. y A. z ( ( x A y /\ x A z ) -> y = z ) ) ) $=
      ( wfun wrel ccnv ccom cid wss wa cv wbr weq wi wal df-fun wex copab
      df-id sseq2i df-co sseq1i ssopab2 3bitr visset brcnv biran biex birim
      19.23v bitr4 bial alcom bitr bilan ) DEDFZDDGZHZIJZKUQALZBLZDMZVACLDMZKZBC
      NZOZCPZBPAPZKDQUTVIUQUTVBVAURMZVDKZARZVFOZCPZBPZVHAPZBPVIUTUSVFBCSZJVLBCS
      ZVQJVOIVQUSBCTUAUSVRVQBCADURUBUCVLVFBCUDUEVNVPBVNVGAPZCPVPVMVSCVMVEARZVFO
      VSVLVTVFVKVEAVJVCVDVBVADBUFAUFUGUHUIUJVEVFAUKULUMVGCAUNUOUMVHBAUNUEUPUO
      $.
      $( [23-Apr-04] $) $( [29-Dec-96] $)

    $( Alternate definition of function. $)
    dffun3 $p |- ( Fun A <-> ( Rel A /\
                 A. x E. z A. y ( x A y -> y = z ) ) ) $=
      ( wfun wrel cv wbr wa weq wi wal wex dffun2 wmo breq2 mo4 ax-17 mo2
      bitr3 bial bilan bitr ) DEDFZAGZBGZDHZUECGZDHZIBCJZKCLBLZALZIUDUGUJKBLCMZ
      ALZIABCDNULUNUDUKUMAUKUGBOUMUGUIBCUFUHUEDPQUGBCUGCRSTUAUBUC $.
      $( [23-Apr-04] $) $( [29-Dec-96] $)

    $( Alternate definition of a function.  Definition 6.4(4) of [TakeutiZaring]
       p. 24. $)
    dffun4 $p |- ( Fun A <-> ( Rel A /\
                 A. x A. y A. z ( ( <. x , y >. e. A /\ <. x , z >. e. A )
                 -> y = z ) ) ) $=
      ( wfun wrel cv wbr wa weq wi wal cop wcel dffun2 df-br bi2an birim bial
      bi2al bilan bitr ) DEDFZAGZBGZDHZUDCGZDHZIZBCJZKZCLZBLALZIUCUDUEMDNZUDUGM
      DNZIZUJKZCLZBLALZIABCDOUMUSUCULURABUKUQCUIUPUJUFUNUHUOUDUEDPUDUGDPQRSTUAU
      B $.
      $( [29-Dec-96] $)

    $( Alternate definition of function. $)
    dffun5 $p |- ( Fun A <-> ( Rel A /\
                 A. x E. z A. y ( <. x , y >. e. A -> y = z ) ) ) $=
      ( wfun wrel cv wbr weq wi wal wex wa cop wcel dffun3 df-br birim bial
      biex bilan bitr ) DEDFZAGZBGZDHZBCIZJZBKZCLZAKZMUCUDUENDOZUGJZBKZCLZAKZMA
      BCDPUKUPUCUJUOAUIUNCUHUMBUFULUGUDUEDQRSTSUAUB $.
      $( [29-Dec-96] $)
  $}

  ${
    $d x y z w v u $.  $d z A w v u $.  $( ` w v u ` are dummy. $)
    dffunmof.1 $e |- ( z e. A -> A. x z e. A ) $.
    dffunmof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Function definition requiring only that ` x ` and ` y ` not be
       "free" in ` A ` (but not necessarily absent from it), using "at most
       one" notation. $)
    dffunmof $p |- ( Fun A <-> ( Rel A /\ A. x E* y x A y ) ) $=
      ( vw vv vu wfun wrel cv wbr weq wi wal wex wa wmo dffun3 wel ax-17 hbbr
      breq2 cbvmo bial mo2 hbmo breq1 bimod cbval 3bitr3r bilan bitr4 ) DJDKZGL
      ZHLZDMZHINOHPIQZGPZRUOALZBLZDMZBSZAPZRGHIDTVEUTUOURHSZGPUPVBDMZBSZGPUTVEV
      FVHGURVGHBBCUPUQDCGUAZBUBFCHUABUBUCVGHUBUQVBUPDUDUEUFVFUSGURHIURIUBUGUFVH
      VDGAVGABACUPVBDVIAUBECBUAAUBUCUHVDGUBGANZVGVCBVJBUBUPVAVBDUIUJUKULUMUN $.
      $( [15-Aug-04] $) $( [9-Mar-95] $)
  $}

  ${
    $d x y z w v u $.  $d z A w v u $.  $( ` w v u ` are dummy. $)
    dffunmofOLD.1 $e |- ( z e. A -> A. x z e. A ) $.
    dffunmofOLD.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Function definition requiring only that ` x ` and ` y ` not be
       "free" in ` A ` (but not necessarily absent from it), using "at most
       one" notation. $)
    dffunmofOLD $p |- ( Fun A <-> ( Rel A /\ A. x E* y <. x , y >. e. A ) ) $=
      ( vw vv vu wfun wrel cv cop wcel weq wi wal wex wa wmo dffun5 ax-17 hbel
      opeq2 eleq1d cbvmo bial mo2 hbmo opeq1 bimod cbval 3bitr3r bilan bitr4 )
      DJDKZGLZHLZMZDNZHIOPHQIRZGQZSUPALZBLZMZDNZBTZAQZSGHIDUAVHVBUPUTHTZGQUQVDM
      ZDNZBTZGQVBVHVIVLGUTVKHBBCCUSDCLZUSNBUBFUCVKHUBHBOUSVJDURVDUQUDUEUFUGVIVA
      GUTHIUTIUBUHUGVLVGGAVKABACCVJDVMVJNAUBEUCUIVGGUBGAOZVKVFBVNBUBVNVJVEDUQVC
      VDUJUEUKULUMUNUO $.
      $( [9-Mar-95] $)
  $}

  ${
    $d x y z A $.  $( ` z ` is dummy. $)
    $( Alternate definition of a function using "at most one" notation. $)
    dffunmo $p |- ( Fun A <-> ( Rel A /\ A. x E* y x A y ) ) $=
      ( vz cv wcel ax-17 dffunmof ) ABDCDECFZAGIBGH $.
      $( [15-Aug-04] $) $( [9-Mar-95] $)

    $( Alternate definition of a function using "at most one" notation. $)
    dffunmoOLD $p |- ( Fun A <-> ( Rel A /\ A. x E* y <. x , y >. e. A ) ) $=
      ( vz cv wcel ax-17 dffunmofOLD ) ABDCDECFZAGIBGH $.
      $( [9-Mar-95] $)

    $( A function has at most one value for each argument. $)
    funmo $p |- ( Fun A -> E* y x A y ) $=
      ( wfun cv wbr wmo wrel wal dffunmo pm3.27bd 19.21bi ) CDZAEBECFBGZAMCHNAI
      ABCJKL $.
      $( [15-Aug-04] $) $( [24-May-98] $)

    $( A function has at most one value for each argument. $)
    funmoOLD $p |- ( Fun A -> E* y <. x , y >. e. A ) $=
      ( wfun cv cop wcel wmo wrel wal dffunmoOLD pm3.27bd 19.21bi ) CDZAEBEFCGB
      HZANCIOAJABCKLM $.
      $( [24-May-98] $)
  $}

  $( A function is a relation. $)
  funrel $p |- ( Fun A -> Rel A ) $=
    ( wfun wrel ccnv ccom cid wss df-fun pm3.26bd ) ABACAADEFGAHI $.
    $( [1-Aug-94] $)

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    $( Subclass theorem for function predicate. $)
    ssfun $p |- ( A (_ B -> ( Fun B -> Fun A ) ) $=
      ( vx vy vz wss wfun wrel cv cop wcel weq wi wal wex wa ssrel funrel syl5
      ssel syl4d 19.20dv 19.22dv dffun5 pm3.27bd jcad syl6ibr ) ABFZBGZAHZCIDIJ
      ZAKZDELZMZDNZEOZCNZPAGUHUIUJUQUHBHZUJUIABQBRSUHUKBKZUMMZDNZEOZCNZUQUIUHVB
      UPCUHVAUOEUHUTUNDUHULUSUMABUKTUAUBUCUBUIURVCCDEBUDUESUFCDEAUDUG $.
      $( [16-Aug-94] $)
  $}

  $( Equality theorem for function predicate. $)
  funeq $p |- ( A = B -> ( Fun A <-> Fun B ) ) $=
    ( wss wa wfun wi wceq wb ssfun im2an ancoms eqss bi 3imtr4 ) ABCZBACZDAEZBE
    ZFZRQFZDZABGQRHPOUAPSOTBAIABIJKABLQRMN $.
    $( [16-Aug-94] $)

  ${
    $d y F $.  $d x y $.
    hbfun.1 $e |- ( y e. F -> A. x y e. F ) $.
    $( Bound-variable hypothesis builder for a function. $)
    hbfun $p |- ( Fun F -> A. x Fun F ) $=
      ( wrel ccnv ccom cid wss wa wal wfun hbrel hbcnv hbco cv wcel ax-17 hbss
      hban df-fun bial 3imtr4 ) CEZCCFZGZHIZJZUHAKCLZUIAKUDUGAABCDMABUFHABCUEDA
      BCDNOBPHQARSTCUAZUIUHAUJUBUC $.
      $( [31-Jan-04] $) $( [30-Jan-04] $)
  $}

  ${
    $d x y z F $.
    $( There is exactly one value of a function. $)
    funeu $p |- ( ( Fun F /\ x F y ) -> E! y x F y ) $=
      ( vz cv wbr wfun weu wa wex weq wi wal 19.8a wrel dffun3 pm3.27bd
      19.21bi im2an ax-17 eu3 sylibr ancoms ) AEBECFZCGZUDBHZUDUEIUDBJZUDBDKLBM
      DJZIUFUDUGUEUHUDBNUEUHAUECOUHAMABDCPQRSUDBDUDDTUAUBUC $.
      $( [24-Apr-04] $) $( [22-Apr-04] $)

    $( There is exactly one value of a function. $)
    funeu2 $p |- ( ( Fun F /\ <. x , y >. e. F ) -> E! y <. x , y >. e. F ) $=
      ( wfun cv wbr wa weu cop wcel funeu df-br bilan bieu 3imtr3 ) CDZAEZBEZCF
      ZGSBHPQRICJZGTBHABCKSTPQRCLZMSTBUANO $.
      $( [3-Aug-94] $)
  $}

  ${
    $d x y A $.
    $( Alternate definition of a function.  One possibility for the definition
       of a function in [Enderton] p. 42.  (Enderton's definition is ambiguous
       because "there is only one" could mean either "there is at most one" or
       "there is exactly one".  However, ~ dffun7 shows that it doesn't matter
       which meaning we pick.)  $)
    dffun6 $p |- ( Fun A <-> ( Rel A /\ A. x e. dom A E* y x A y ) ) $=
      ( wfun wrel cv cop wcel wmo wal wa wbr cdm wral dffunmoOLD wi wex moabs
      visset eldm2 df-br bimo bi2im bitr4 bial df-ral bilan bitr ) CDCEZAFZBFZG
      CHZBIZAJZKUIUJUKCLZBIZACMZNZKABCOUNURUIUNUJUQHZUPPZAJURUMUTAUMULBQZUMPUTU
      LBRUSVAUPUMBUJCASTUOULBUJUKCUAUBUCUDUEUPAUQUFUDUGUH $.
      $( [4-Nov-02] $)

    $( Alternate definition of a function.  One possibility for the
       definition of a function in [Enderton] p. 42.  Compare ~ dffun6 . $)
    dffun7 $p |- ( Fun A <-> ( Rel A /\ A. x e. dom A E! y x A y ) ) $=
      ( wfun wrel cv wbr weu cdm wral wa funrel cop wcel wex ax-17 hbeu1
      funeu2 exp 19.23ad visset eldm2 df-br bieu 3imtr4g r19.21aiv jca wmo
      eumo r19.20si imlan dffun6 sylibr impbi ) CDZCEZAFZBFZCGZBHZACIZJZKZUOUPV
      BCLUOUTAVAUOUQURMCNZBOVDBHZUQVANUTUOVDVEBUOBPVDBQUOVDVEABCRSTBUQCAUAUBUSV
      DBUQURCUCUDUEUFUGVCUPUSBUHZAVAJZKUOVBVGUPUTVFAVAUSBUIUJUKABCULUMUN $.
      $( [4-Nov-02] $)
  $}

  $( An equivalence for the function predicate. $)
  funfn $p |- ( Fun A <-> A Fn dom A ) $=
    ( wfun cdm wceq wa wfn cleqid biantru df-fn bitr4 ) ABZKACZLDZEALFMKLGHALIJ
    $.
    $( [?] $) $( [13-Aug-04] $)

  ${
    $d x y z A $.  $d x y z B $.  $( ` x y z ` are dummy. $)
    funsn.1 $e |- A e. V $.
    funsn.2 $e |- B e. V $.
    $( A singleton of an ordered pair is a function. $)
    funsn $p |- Fun { <. A , B >. } $=
      ( vx vy vz cop csn wfun wrel cv wcel wa weq wi wal relsn wceq cleq2
      biimparc opex elsnc visset opth pm3.27bd sylbi syl2an ax-gen gen2 pm3.2i
      dffun4 mpbir ) ABHZIZJUOKZELZFLZHZUOMZUQGLZHZUOMZNFGOZPZGQZFQEQZNUPVGABCR
      VFEFVEGURBSZVABSZVDUTVCVIVDVHVABURTUAUTUSUNSZVHUSUNUQURUBUCVJUQASZVHUQURA
      BEUDZFUDDUEUFUGVCVBUNSZVIVBUNUQVAUBUCVMVKVIUQVAABVLGUDDUEUFUGUHUIUJUKEFGU
      OULUM $.
      $( [12-Aug-94] $)
  $}

  $( The empty set is a function. $)
  fun0 $p |- Fun (/) $=
    ( c0 cop csn wss wfun 0ss 0ex funsn ssfun mp2 ) AAABCZDKEAEKFAAGGHAKIJ $.
    $( [7-Apr-98] $)

  $( The identity relation is a function. $)
  funi $p |- Fun I $=
    ( cid wfun wrel ccnv ccom wss wa reli wceq relcnv coi2 ax-mp cnvi eqtr
    ssid eqsstr pm3.2i df-fun mpbir ) ABACZAADZEZAFZGTUCHUBAAUBUAAUACUBUAIAJUAK
    LMNAOPQARS $.
    $( [30-Apr-98] $)

  $( The universe is not a function.  (Contributed by Raph Levien,
     27-Jan-04.) $)
  nfunv $p |- -. Fun V $=
    ( cvv wfun wrel c0 cxp wcel 0nelxp 0ex wss df-rel biimp sseld mpi mto
    funrel ) ABACZPDAAEZFZAAGPDAFRHPAQDPAQIAJKLMNAON $.
    $( [27-Jan-04] $) $( [27-Jan-04] $)

  ${
    $d x y z $.  $d z ph $.  $( ` z ` is dummy. $)
    $( A class of ordered pairs is a function when there is at most one
       second member for each pair. $)
    funopab $p |- ( Fun { <. x , y >. | ph } <-> A. x E* y ph ) $=
      ( vz copab wfun cv cop wcel wmo wal wrel hbopab1 hbopab2 dffunmofOLD
      relopab mpbiran opabid bimo bial bitr ) ABCEZFZBGCGHUBIZCJZBKZACJZBKUCUBL
      UFBCDUBABCDMABCDNOABCPQUEUGBUDACABCRSTUA $.
      $( [16-May-95] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( A class of ordered pairs of values is a function. $)
    funopabeq $p |- Fun { <. x , y >. | y = A } $=
      ( cv wceq copab wfun wmo funopab moeq mpgbir ) BDCEZABFGLBHALABIBCJK $.
      $( [14-Nov-95] $)
  $}

  ${
    $d x y z F $.  $d x y z G $. $( ` x y z ` are dummy. $)
    $( The composition of two functions is a function.  Exercise 29 of
       [TakeutiZaring] p. 25. $)
    funco $p |- ( ( Fun F /\ Fun G ) -> Fun ( F o. G ) ) $=
      ( vx vy vz wfun wa ccom wrel cv cop wcel wmo wal wbr wex moexexv
      dffunmoOLD pm3.27bd 19.21bi df-br bimo sylibr bial syl2an ancoms visset
      opelco 19.21aiv relco jctil ) AFZBFZGZABHZIZCJZDJZKUOLZDMZCNZGUOFUNVAUPUN
      UTCUNUQEJZBOZVBURAOZGEPZDMZUTUMULVFVCEMZVDDMZENZVFUMULVCVDEDQUMUQVBKBLZEM
      ZVGUMVKCUMBIVKCNCEBRSTVCVJEUQVBBUAUBUCULVBURKALZDMZENZVIULAIVNEDARSVHVMEV
      DVLDVBURAUAUBUDUCUEUFUSVEDEUQURABCUGDUGUHUBUCUIABUJUKCDUORUC $.
      $( [26-Jan-97] $)
  $}

  ${
    $d x y z F $.  $d x y z A $.  $( ` x y z ` are dummy. $)
    $( A restriction of a function is a function.  Compare Exercise 18 of
       [TakeutiZaring] p. 25. $)
    funres $p |- ( Fun F -> Fun ( F |` A ) ) $=
      ( cres wss wfun wi resss ssfun ax-mp ) BACZBDBEJEFBAGJBHI $.
      $( [16-Aug-94] $)
  $}

  ${
    $d x y F $.  $d x y G $.  $d x y A $.
    $( The restriction of a function to the domain of a subclass equals the
       subclass. $)
    funssres $p |- ( ( Fun F /\ G (_ F ) -> ( F |` dom G ) = G ) $=
      ( vx vy wfun wss wa cdm cres wceq cv cop wcel wb wal wi ssel visset
      opeldm a1i jcad adantl weu wex eupick funeu2 ancrd 19.22dv eldm2 syl5ib
      imp syl2an exp43 com23 com34 pm2.43d imp3a impbid opelres syl6rbbr
      19.21aivv wrel ssrel funrel syl5 com12 relres jctil cleqrel syl mpbird )
      AEZBAFZGZABHZIZBJZCKZDKZLZVPMZVTBMZNZDOCOZVNWCCDVNWBVTAMZVRVOMZGZWAVNWBWG
      VMWBWGPVLVMWBWEWFBAVTQZWBWFPVMVRVSBCRZSTUAUBVNWEWFWBVNWEWFWBPVNWEWFWEWBVL
      VMWEWFWEWBPZPZPVLWEVMWKVLWEVMWFWJWEDUCWEWBGZDUDZWJVLWEGVMWFGWEWBDUECDAUFV
      MWFWMVMWBDUDWMWFVMWBWLDVMWBWEWHUGUHDVRBWIUIUJUKULUMUNUKUOUPUQURVRVSAVODRU
      SUTVAVNVPVBZBVBZGVQWDNVNWOWNVLVMWOVMVLWOVMAVBWOVLBAVCAVDVEVFUKAVOVGVHCDVP
      BVIVJVK $.
      $( [19-Mar-04] $) $( [15-Aug-94] $)
  $}

  $( Equality of restrictions of a function and a subclass. $)
  fun2ssres $p |- ( ( ( Fun F /\ G (_ F ) /\ A (_ dom G ) ->
                  ( F |` A ) = ( G |` A ) ) $=
    ( wfun wss wa cdm cres wceq funssres reseq1 syl cleqcomd resabs1 sylan9eq
    ) BDCBEFZACGZEZFCAHZBAHZPRSBQHZAHZTPUBSPUACIUBSIBCJUACAKLMBAQNOM $.
    $( [16-Aug-94] $)

  ${
    $d x y z F $.  $d x y z G $.  $( ` x y z ` are dummy. $)
    $( The union of functions with disjoint domains is a function.  Theorem
       4.6 of [Monk1] p. 43. $)
    funun $p |- ( ( ( Fun F /\ Fun G ) /\ ( dom F i^i dom G ) = (/) ) ->
                Fun ( F u. G ) ) $=
      ( vx vy vz wfun wa cdm cin c0 wceq cun wrel cv cop wcel weq wi wal
      funrel im2an relun sylibr adantr wo wn disj1 biimp 19.21bi imnan sylib
      visset opeldm nsyl orel2 syl con2d orel1 im2ord adantl elun bi2an anddi
      bitr syl5ib dffun4 pm3.27bd 19.21bbi jaao syld 19.21aiv 19.21aivv jca )
      AFZBFZGZAHZBHZIJKZGZABLZMZCNZDNZOZWAPZWCENZOZWAPZGZDEQZRZESZDSCSZGWAFVTWB
      WNVPWBVSVPAMZBMZGWBVNWOVOWPATBTUAABUBUCUDVTWMCDVTWLEVTWJWEAPZWHAPZGZWEBPZ
      WHBPZGZUEZWKVTWSWQXAGZUEZWTWRGZXBUEZUEZXCWJVSXHXCRVPVSXEWSXGXBVSXDUFXEWSR
      VSWCVQPZWCVRPZGZXDVSXIXJUFRZXKUFVSXLCVSXLCSCVQVRUGUHUIZXIXJUJUKWQXIXAXJWC
      WDACULZUMWCWGBXNUMUAUNXDWSUOUPVSXFUFXGXBRVSXJXIGZXFVSXJXIUFRXOUFVSXIXJXMU
      QXJXIUJUKWTXJWRXIWCWDBXNUMWCWGAXNUMUAUNXFXBURUPUSUTWJWQWTUEZWRXAUEZGXHWFX
      PWIXQWEABVAWHABVAVBWQWTWRXAVCVDVEVPXCWKRVSVNWSWKVOXBVNWSWKRZDEVNXRESDSZCV
      NWOXSCSCDEAVFVGUIVHVOXBWKRZDEVOXTESDSZCVOWPYACSCDEBVFVGUIVHVIUDVJVKVLVMCD
      EWAVFUC $.
      $( [12-Aug-94] $)
  $}

  ${
    $d f g x y z w v A $.
    $( A simpler definition of single-rooted (see ~ funcnv ). $)
    funcnv2 $p |- ( Fun `' A <-> A. y E* x x A y ) $=
      ( ccnv wfun wrel cv wbr wmo wal wa dffunmo relcnv biantrur visset brcnv
      bimo bial bitr3 bitr ) CDZEUAFZBGZAGZUAHZAIZBJZKZUDUCCHZAIZBJZBAUALUHUGUK
      UBUGCMNUFUJBUEUIAUCUDCBOAOPQRST $.
      $( [?] $) $( [9-Aug-04] $)

    $( The converse of a set is a function iff the set is single-rooted, which
       means that for any ` y ` in the range of ` A ` there is at most one
       ` x ` such that ` x A y ` .  Definition of single-rooted in [Enderton]
       p. 43.  See ~ funcnv2 for a simpler version. $)
    funcnv $p |- ( Fun `' A <-> A. y e. ran A E* x x A y ) $=
      ( cv wbr wmo wal crn wcel wi ccnv wfun wral wa visset brelrn pm4.71ri
      bimo moanimv bitr bial funcnv2 df-ral 3bitr4 ) ADZBDZCEZAFZBGUFCHZIZUHJZB
      GCKLUHBUIMUHUKBUHUJUGNZAFUKUGULAUGUJUEUFCAOBOPQRUJUGASTUAABCUBUHBUIUCUD
      $.
      $( [?] $) $( [13-Aug-04] $)

    $( The double converse of a set is a function iff the set is single-valued.
       Each side is equivalent to Definition 6.4(2) of [TakeutiZaring] p. 23,
       who use the notation "Un(A)" for single-valued.  Note that ` A ` is not
       necessarily a function. $)
    fun2cnv $p |- ( Fun `' `' A <-> A. x E* y x A y ) $=
      ( ccnv wfun cv wbr wmo wal funcnv2 visset brcnv bimo bial bitr ) CDZDEBFZ
      AFZPGZBHZAIRQCGZBHZAIBAPJTUBASUABQRCBKAKLMNO $.
      $( [?] $) $( [13-Aug-04] $)

    $( The union of a chain (with respect to inclusion) of functions is a
       function. $)
    fununi $p |- ( A. f e. A ( Fun f /\ A. g e. A ( f (_ g \/ g (_ f ) ) ->
                 Fun U. A ) $=
      ( vx vy vz vw vv cv wfun wss wo wral wa cuni wrel cop wcel weq wi wal
      funrel adantr r19.20si reluni sylibr r19.28av ssel imrand dffun4
      pm3.27bd ax-4 a4s 3syl syl9r adantl imland jaod imp funeq sseq1 sseq2
      bi2ord bi2and biland cbvral2v ralcom orcom syl5bb bitr bi2an anidm
      anandir bi2ral r19.26-2 bitr2 3bitr3 wex eluni eeanv an4 ancom bi2ex
      bitr3 birim 19.23v bial impexp bi2al r2al bitr4 3bitr3r 3imtr4 19.21aiv
      19.21aivv syl jca ) BIZJZWRCIZKZWTWRKZLZCAMZNZBAMZAOZPZDIZEIQZXGRZXIFIQZX
      GRZNZEFSZTZFUAZEUADUAZNXGJXFXHXRXFWRPZBAMXHXEXSBAWSXSXDWRUBUCUDBAUEUFXFWS
      XCNZCAMZBAMZXRXEYABAWSXCCAUGUDYBXQDEYBXPFGIZJZHIZJZNZYCYEKZYEYCKZLZNZHAMZ
      GAMZXJYCRZXLYERZNZXOTZHAMZGAMZYBXPYLYRGAYKYQHAYGYJYQYGYHYQYIYFYHYQTYDYHYP
      XJYERZYONZYFXOYHYNYTYOYCYEXJUHUIYFUVAXOTZFUAZEUAZDUAZUVCUVBYFYEPUVEDEFYEU
      JUKUVDUVCDUVCEULUMUVBFULUNUOUPYDYIYQTYFYIYPYNXLYCRZNZYDXOYIYOUVFYNYEYCXLU
      HUQYDUVGXOTZFUAZEUAZDUAZUVIUVHYDYCPUVKDEFYCUJUKUVJUVIDUVIEULUMUVHFULUNUOU
      CURUSUDUDYBYBNYDYJNZHAMGAMZYFYJNZHAMGAMZNZYBYMYBUVMYBUVOXTUVLYDYCWTKZWTYC
      KZLZNBCGHAABGSZWSYDXCUVSWRYCUTUVTXAUVQXBUVRWRYCWTVAWRYCWTVBVCVDCHSZUVSYJY
      DUWAUVQYHUVRYIWTYEYCVBWTYEYCVAVCVEVFYBXTBAMCAMUVOXTBCAAVGXTUVNWSYCWRKZWRY
      CKZLZNCBGHAACGSZXCUWDWSUWEXBXALUWDXCUWEXBUWBXAUWCWTYCWRVAWTYCWRVBVCXAXBVH
      VIVEBHSZWSYFUWDYJWRYEUTUWFUWBYHUWCYIWRYEYCVBWRYEYCVAVCVDVFVJVKYBVLYMUVLUV
      NNZHAMGAMUVPYKUWGGHAAYDYFYJVMVNUVLUVNGHAAVOVPVQXPYCARZYEARZNZYPNZHVRZGVRZ
      XOTZYSXNUWMXOXNYNUWHNZGVRZYOUWINZHVRZNZUWMXKUWPXMUWRGXJAVSHXLAVSVKUWSUWOU
      WQNZHVRGVRUWMUWOUWQGHVTUWTUWKGHUWTYPUWJNUWKYNUWHYOUWIWAYPUWJWBVJWCWDVJWEU
      WKXOTZHUAZGUAZUWLXOTZGUAYSUWNUXBUXDGUWKXOHWFWGUXCUWJYQTZHUAGUAYSUXAUXEGHU
      WJYPXOWHWIYQGHAAWJWKUWLXOGWFWLVJWMWNWOWPWQDEFXGUJUF $.
      $( [?] $) $( [10-Aug-04] $)

    $( The union of a chain (with respect to inclusion) of single-rooted sets
       is single-rooted.  (See ~ funcnv for "single-rooted" definition.) $)
    funcnvuni $p |- ( A. f e. A ( Fun `' f /\ A. g e. A ( f (_ g \/ g (_ f ) )
                    -> Fun `' U. A ) $=
      ( vy vx vz vw vv cv ccnv wfun wss wo wral wa wceq wrex cab cuni wi wal
      wcel weq wb cnveq funeq syl sseq1 sseq2 bi2ord biraldv bi2and rcla4v
      biimprcd adantr sseq12 ancoms cnvss im2or syl5bir com12 exp3a syl6
      r19.23adv com23 19.21adv adantl jcad cleq2d cbvrexv syl5ib 19.21aiv
      df-ral visset cleq1 birexdv elab birim bial bitr bilan bi2im bitr2 sylib
      fununi ciun cnvuni cnvex dfiun2 eqtr ax-mp sylibr ) BIZJZKZWMCIZLZWPWMLZM
      ZCANZOZBANZDIZEIZJZPZEAQZDRZSZKZASJZKZXBFIZKZXMGIZLZXOXMLZMZGXHNZOZFXHNZX
      JXBXMXEPZEAQZXNXOXEPZEAQZXRTZGUAZOZTZFUAZYAXBYIFXBXMHIZJZPZHAQYHYCXBYMYHH
      AXBYKAUBYLKZYKWPLZWPYKLZMZCANZOZYMYHTXAYSBYKABHUCZWOYNWTYRYTWNYLPWOYNUDWM
      YKUEWNYLUFUGYTWSYQCAYTWQYOWRYPWMYKWPUHWMYKWPUIUJUKULUMYSYMXNYGYNYMXNTYRYM
      XNYNXMYLUFUNUOYRYMYGTYNYRYMYFGYRYEYMXRYRYDYMXRTZEAYRXDAUBYKXDLZXDYKLZMZYD
      UVATYQUVDCXDACEUCYOUVBYPUVCWPXDYKUIWPXDYKUHUJUMUVDYDYMXRYDYMOZUVDXRUVEXRY
      LXELZXEYLLZMUVDUVEXPUVFXQUVGYMYDXPUVFUDXMYLXOXEUPUQXOXEXMYLUPUJUVBUVFUVCU
      VGYKXDURXDYKURUSUTVAVBVCVDVEVFVGVHVCVDYBYMEHAEHUCXEYLXMXDYKUEVIVJVKVLYAXM
      XHUBZXTTZFUAYJXTFXHVMUVIYIFUVHYCXTYHXGYCDXMFVNDFUCXFYBEAXCXMXEVOVPVQXSYGX
      NXSXOXHUBZXRTZGUAYGXRGXHVMUVKYFGUVJYEXRXGYEDXOGVNDGUCXFYDEAXCXOXEVOVPVQVR
      VSVTWAWBVSWCWDXHFGWEUGXKXIPXLXJUDXKEAXEWFXIEAWGEDAXEXDEVNWHWIWJXKXIUFWKWL
      $.
      $( [?] $) $( [11-Aug-04] $)

    $( The union of a chain (with respect to inclusion) of one-to-one functions
       is a one-to-one function. $)
    fun11uni $p |- ( A. f e. A ( ( Fun f /\ Fun `' f ) /\
                   A. g e. A ( f (_ g \/ g (_ f ) ) ->
                   ( Fun U. A /\ Fun `' U. A ) ) $=
      ( cv wfun ccnv wa wss wo wral cuni pm3.26 imran r19.20si fununi syl
      pm3.27 funcnvuni jca ) BDZEZTFEZGZTCDZHUDTHICAJZGZBAJZAKZEZUHFEZUGUAUEGZB
      AJUIUFUKBAUCUAUEUAUBLMNABCOPUGUBUEGZBAJUJUFULBAUCUBUEUAUBQMNABCRPS $.
      $( [?] $) $( [11-Aug-04] $)
  $}

  ${
    $d x y F $.  $d x y G $.
    $( The intersection with a function is a function.  Exercise 14(a) of
       [Enderton] p. 53. $)
    funin $p |- ( Fun F -> Fun ( F i^i G ) ) $=
      ( vx vy wrel cv cop wcel wmo wal wa cin wfun relin moan ancom elin bitr4
      bimo sylib 19.20i im2an dffunmoOLD 3imtr4 ) AEZCFDFGZAHZDIZCJZKABLZEZUFUJ
      HZDIZCJZKAMUJMUEUKUIUNABNUHUMCUHUFBHZUGKZDIUMUGUODOUPULDUPUGUOKULUOUGPUFA
      BQRSTUAUBCDAUCCDUJUCUD $.
      $( [20-Mar-04] $) $( [19-Mar-04] $)
  $}

  $( The restriction of a one-to-one function is one-to-one. $)
  funres11 $p |- ( Fun `' F -> Fun `' ( F |` A ) ) $=
    ( cres ccnv wss wfun wi resss cnvss ax-mp ssfun ) BACZDZBDZEZNFMFGLBEOBAHLB
    IJMNKJ $.
    $( [25-Mar-98] $)

  $( The converse of a restricted function. $)
  funcnvres $p |- ( Fun `' F -> `' ( F |` A ) = ( `' F |` ( F " A ) ) ) $=
    ( ccnv wfun cres cdm cima wss wceq resss cnvss ax-mp funssres mpan2 crn
    df-ima df-rn eqtr reseq2 syl5req ) BCZDZUABAEZCZFZEZUDUABAGZEZUBUDUAHZUFUDI
    UCBHUIBAJUCBKLUAUDMNUGUEIUHUFIUGUCOUEBAPUCQRUGUEUASLT $.
    $( [27-Mar-98] $)

  $( The image of the converse image of a function. $)
  funimacnv $p |- ( Fun F -> ( F " ( `' F " A ) ) = ( A i^i ran F ) ) $=
    ( wfun ccnv cima cres crn cin wceq wrel funrel dfrel2OLD sylib funeq
    biimpd mpcom funcnvres syl reseq1 eqtr4d rneqd df-ima syl6reqr cdm dfdm4
    dmres df-rn ineq2i eqtr4 eqtr3 syl6eq ) BCZBBDZAEZEZUMAFZDZGZABGZHZULURBUNF
    ZGUOULUQVAULUQUMDZUNFZVAULVBCZUQVCIBVBIZULVDULBJVEBKBLMZVEULVDBVBNOPAUMQRUL
    VEVAVCIVFBVBUNSRTUABUNUBUCUPUDZURUTUPUEVGAUMUDZHUTUMAUFUSVHABUGUHUIUJUK $.
    $( [7-Jun-04] $) $( [25-May-04] $)

  $( A kind of contraposition law that infers a subclass of an image from
     a converse image subclass. $)
  funimass1 $p |- ( ( Fun F /\ A (_ ran F ) ->
                 ( ( `' F " A ) (_ B -> A (_ ( F " B ) ) ) $=
    ( wfun crn wss wa ccnv cima cin funimacnv wceq dfss biimp cleqcomd
    sylan9eq sseq1d imass2 syl5bi ) CDZACEZFZGZCCHAIZIZCBIZFAUFFUDBFUCUEAUFTUBU
    EAUAJZAACKUBAUGUBAUGLAUAMNOPQUDBCRS $.
    $( [10-Jun-04] $) $( [25-May-04] $)

  $( A kind of contraposition law that infers an image subclass from a subclass
     of a converse image. $)
  funimass2 $p |- ( Fun F -> ( A (_ ( `' F " B ) -> ( F " A ) (_ B ) ) $=
    ( wfun cima ccnv wss crn cin funimacnv sseq2d inss1 sstr mpi syl6bi imass2
    syl5 ) CDZCAEZCCFBEZEZGZSBGZATGRUBSBCHZIZGZUCRUAUESBCJKUFUEBGUCBUDLSUEBMNOA
    TCPQ $.
    $( [10-Jun-04] $) $( [25-May-04] $)

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $( ` x y ` are dummy. $)
    $( The image of a difference is the difference of images. $)
    imadif $p |- ( Fun `' F -> ( F " ( A \ B ) ) = ( ( F " A ) \ ( F " B ) ) ) $=
      ( vy vx ccnv wfun cdif cima cv wcel cop wa wex wn wal ax-17 hbe1 hban wi
      wmo mopick funmoOLD visset opelcnv bimo sylib sylan con2d imnan 19.21ai
      exp exancom alnex 3imtr3g imland anandir biex 19.40 sylbi syl5 19.29r
      sylan5br wo andi ianor bilan an23 pm3.24 intnan anass mtbir biorfi bitr
      3bitr4 a1i impbid eldif biran elima3 bineg bi2an 3bitr4g cleqrd ) CFZGZDC
      ABHZIZCAIZCBIZHZWFEJZWGKZWLDJZLCKZMZENZWNWIKZWNWJKZOZMZWNWHKWNWKKWFWLAKZW
      LBKZOZMZWOMZENZXBWOMZENZXCWOMZENZOZMZWQXAWFXGXMWFXIXDWOMZENZMZXMXGWFXOXLX
      IWFWOXDMZENZXJOZEPZXOXLWFXRXTWFXRMZXSEWFXREWFEQXQERSYAXCWOOZTXSYAWOXCWOEU
      AZXRWOXDTWFWOXDEUBWFWNWLLWEKZEUAYCDEWEUCYDWOEWNWLCDUDZEUDUEUFUGUHUIXCWOUJ
      UGUKULWOXDEUMXJEUNZUOUPXGXHXNMZENXPXFYGEXBXDWOUQURXHXNEUSUTVAXMXGTWFXMXHX
      SMZENZXGXIXTYIXLXHXSEVBYFVCYHXFEXHXDYBVDZMXHXDMZXHYBMZVDZYHXFXHXDYBVEXSYJ
      XHXCWOVFVGXFYKYMXBXDWOVHYLYKYLXBWOYBMZMYNXBWOVIVJXBWOYBVKVLVMVNVOURUGVPVQ
      WPXFEWMXEWOWLABVRVSURWRXIWTXLEWNCAYEVTWSXKEWNCBYEVTWAWBWCEWNCWGYEVTWNWIWJ
      VRWCWD $.
      $( [24-May-98] $)
  $}

  ${
    $d w B $.  $d x y z w A $.  $( ` x y z w ` are dummy. $)
    $( Axiom of Replacement using abbreviations.  Axiom 39(vi) of [Quine]
       p. 284.  Compare Exercise 9 of [TakeutiZaring] p. 29. $)
    funimaexg $p |- ( B e. C -> ( Fun A -> ( A " B ) e. V ) ) $=
      ( vw vx vy vz wfun cv cima cvv wcel wi wceq imaeq2 eleq1d bilimd cop weq
      wal wex wrel dffun5 pm3.27bd wel wa wb ax-17 zfrep2 isset cab dfima3
      cleq2i cleqab bitr biex sylibr syl vtoclg ) AHZADIZJZKLZMUTABJZKLZMDBCVAB
      NZVCVEUTVFVBVDKVABAOPQUTEIFIRALZFGSMFTGUAETZVCUTAUBVHEFGAUCUDVHFGUEEDUEVG
      UFEUAZUGFTZGUAZVCVGEFGDVGGUHUIVCGIZVBNZGUAVKGVBUJVMVJGVMVLVIFUKZNVJVBVNVL
      EFAVAULUMVIFVLUNUOUPUOUQURUS $.
      $( [14-Aug-94] $)
  $}

  ${
    zfrep5.1 $e |- B e. V $.
    $( The image of a set under any function is also a set.  Equivalent of
       Axiom of Replacement ~ ax-rep .  Axiom 39(vi) of [Quine] p. 284.
       Compare Exercise 9 of [TakeutiZaring] p. 29. $)
    funimaex $p |- ( Fun A -> ( A " B ) e. V ) $=
      ( cvv wcel wfun cima wi funimaexg ax-mp ) BDEAFABGDEHCABDIJ $.
      $( [17-Nov-02] $)
  $}

  $( The restriction of a function to a set exists.  Compare Proposition
     6.17 of [TakeutiZaring] p. 28. $)
  resfunexg $p |- ( B e. C -> ( Fun A -> ( A |` B ) e. V ) ) $=
    ( wcel wfun cres cvv wa cdm crn cxp dmresexg adantr cima funimaexg df-ima
    eleq1i syl6ib imp jca xpexg wss wrel relres relssdr ax-mp ssexg mpi 3syl
    exp ) BCDZAEZABFZGDZUKULHZUMIZGDZUMJZGDZHUPURKZGDZUNUOUQUSUKUQULABCLMUKULUS
    UKULABNZGDUSABCOVBURGABPQRSTUPURGGUAVAUMUTUBZUNUMUCVCABUDUMUEUFUMUTGUGUHUIU
    J $.
    $( [7-Apr-95] $)

  $( Equality theorem for function predicate with domain. $)
  fneq1 $p |- ( F = G -> ( F Fn A <-> G Fn A ) ) $=
    ( wceq wfun cdm wa wfn funeq dmeq cleq1d bi2and df-fn 3bitr4g ) BCDZBEZBFZA
    DZGCEZCFZADZGBAHCAHOPSRUABCIOQTABCJKLBAMCAMN $.
    $( [1-Aug-94] $)

  $( Equality theorem for function predicate with domain. $)
  fneq2 $p |- ( A = B -> ( F Fn A <-> F Fn B ) ) $=
    ( wceq wfun cdm wa wfn cleq2 biland df-fn 3bitr4g ) ABDZCEZCFZADZGNOBDZGCAH
    CBHMPQNABOIJCAKCBKL $.
    $( [1-Aug-94] $)

  ${
    $d y F $.  $d y A $.  $d x y $.
    hbfn.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfn.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for a function with domain. $)
    hbfn $p |- ( F Fn A -> A. x F Fn A ) $=
      ( wfun cdm wceq wa wal wfn hbfun hbdm hbeq hban df-fn bial 3imtr4 ) DGZDH
      ZCIZJZUCAKDCLZUDAKTUBAABDEMABBUACABDENFOPDCQZUDUCAUERS $.
      $( [31-Jan-04] $) $( [30-Jan-04] $)
  $}

  $( A function with domain is a function. $)
  fnfun $p |- ( F Fn A -> Fun F ) $=
    ( wfn wfun cdm wceq df-fn pm3.26bd ) BACBDBEAFBAGH $.
    $( [1-Aug-94] $)

  $( A function with domain is a relation. $)
  fnrel $p |- ( F Fn A -> Rel F ) $=
    ( wfn wfun wrel fnfun funrel syl ) BACBDBEABFBGH $.
    $( [1-Aug-94] $)

  $( The domain of a function. $)
  fndm $p |- ( F Fn A -> dom F = A ) $=
    ( wfn wfun cdm wceq df-fn pm3.27bd ) BACBDBEAFBAGH $.
    $( [2-Aug-94] $)

  ${
    funfni.1 $e |- ( ( Fun F /\ B e. dom F ) -> ph ) $.
    $( Inference to convert a function and domain antecedent. $)
    funfni $p |- ( ( F Fn A /\ B e. A ) -> ph ) $=
      ( wfun cdm wcel wfn wa fnfun adantr fndm eleq2d biimpar sylan13 ) DFZCDGZ
      HZADBIZCBHZJETQUABDKLTSUATRBCBDMNOP $.
      $( [25-Apr-04] $) $( [22-Apr-04] $)
  $}

  ${
    $d x F $.  $d x A $.  $d x B $.
    $( A function has a unique domain. $)
    fndmu $p |- ( ( F Fn A /\ F Fn B ) -> A = B ) $=
      ( wfn cdm fndm cleqcomd sylan9eq ) CADZCBDACEZBIJAACFGBCFH $.
      $( [11-Aug-94] $)
  $}

  ${
    fnbr.1 $e |- B e. V $.
    fnbr.2 $e |- C e. V $.
    $( The first argument of binary relation on a function belongs to the
       function's domain. $)
    fnbr $p |- ( ( F Fn A /\ B F C ) -> B e. A ) $=
      ( wfn cdm wcel wbr fndm eleq2d biimpa breldm sylan5 ) DAGZBDHZIZBAIZBCDJP
      RSPQABADKLMBCDENO $.
      $( [8-May-04] $) $( [7-May-04] $)

    $( The first argument of an ordered pair in a function belongs to the
       function's domain. $)
    fnop $p |- ( ( F Fn A /\ <. B , C >. e. F ) -> B e. A ) $=
      ( wfn wbr wcel cop fnbr df-br sylan5br ) DAGBCDHBAIBCJDIABCDEFKBCDLM $.
      $( [8-Aug-94] $)
  $}

  ${
    $d x y F $.  $d x y B $.  $d x A $.  $( ` x ` is dummy. $)
    $( There is exactly one value of a function. $)
    fneu $p |- ( ( F Fn A /\ B e. A ) -> E! y B F y ) $=
      ( vx wfn wcel cv wbr weu wa wi wceq eleq1 biland breq1 bieudv bi2imd wex
      cdm fndm eleq2d visset eldm syl5bbr wfun ax-17 hbeu1 hbim funeu exp
      com12 19.23ai fnfun syl5 syl6bir pm2.43a imp vtoclg anabsi7 ) DBFZCBGZCAH
      ZDIZAJZVAEHZBGZKZVFVCDIZAJZLVAVBKZVELECBVFCMZVHVKVJVEVLVGVBVAVFCBNOVLVIVD
      AVFCVCDPQRVAVGVJVGVAVJVAVGVIASZVAVJLVAVFDTZGVGVMVAVNBVFBDUAUBAVFDEUCUDUEV
      MDUFZVJVAVIVOVJLAVOVJAVOAUGVIAUHUIVOVIVJVOVIVJEADUJUKULUMBDUNUOUPUQURUSUT
      $.
      $( [24-Apr-04] $) $( [22-Apr-04] $)

    $( There is exactly one value of a function. $)
    fneu2 $p |- ( ( F Fn A /\ B e. A ) -> E! y <. B , y >. e. F ) $=
      ( wfn wcel wa cv wbr weu cop fneu df-br bieu sylib ) DBECBFGCAHZDIZAJCPKD
      FZAJABCDLQRACPDMNO $.
      $( [7-Nov-95] $)
  $}

  $( A function does not change when restricted to its domain. $)
  fnresdm $p |- ( F Fn A -> ( F |` A ) = F ) $=
    ( wrel cdm wss cres wceq wfn relssres fnrel fndm eqimss syl sylan13 ) BCBDZ
    AEZBAFBGBAHZBAIABJQOAGPABKOALMN $.
    $( [7-Sep-04] $) $( [5-Sep-04] $)

  $( Membership in two functions restricted by each other's domain. $)
  2elresin $p |- ( ( F Fn A /\ G Fn B ) ->
                 ( ( <. x , y >. e. F /\ <. x , z >. e. G ) <->
                   ( <. x , y >. e. ( F |` ( A i^i B ) ) /\
                     <. x , z >. e. ( G |` ( A i^i B ) ) ) ) ) $=
    ( wfn wa cv cop wcel cin cres wi visset fnop im2an an4 elin 3imtr4 opres
    bi2and biimprd syl exp pm2.43d resss sseli a1i impbid ) FDHZGEHZIZAJZBJZKZF
    LZUOCJZKZGLZIZUQFDEMZNZLZUTGVCNZLZIZUNVBVHUNVBVBVHOZUNVBIZUOVCLZVIULURIZUMV
    AIZIUODLZUOELZIVJVKVLVNVMVODUOUPFAPZBPZQEUOUSGVPCPZQRULUMURVASUODETUAVKVHVB
    VKVEURVGVAUOUPFVCVQUBUOUSGVCVRUBUCUDUEUFUGVHVBOUNVEURVGVAVDFUQFVCUHUIVFGUTG
    VCUHUIRUJUK $.
    $( [8-Aug-94] $)


  $( A restricted function has a restricted domain. $)
  ssfnres $p |- ( A (_ B -> ( F Fn B -> ( F |` A ) Fn A ) ) $=
    ( wss wfun cdm wceq wa cres wfn wi funres a1i cin ineq2 cleq1d dmres
    cleq1i df-ss 3bitr4g biimprcd im2and df-fn 3imtr4g ) ABDZCEZCFZBGZHCAIZEZUI
    FZAGZHCBJUIAJUEUFUJUHULUFUJKUEACLMUHULUEUHAUGNZAGABNZAGULUEUHUMUNAUGBAOPUKU
    MACAQRABSTUAUBCBUCUIAUCUD $.
    $( [2-Aug-94] $)

  $( Restriction of a function's domain with an intersection. $)
  fnresin1 $p |- ( F Fn A -> ( F |` ( A i^i B ) ) Fn ( A i^i B ) ) $=
    ( cin wss wfn cres wi inss1 ssfnres ax-mp ) ABDZAECAFCLGLFHABILACJK $.
    $( [9-Aug-94] $)

  $( Restriction of a function's domain with an intersection. $)
  fnresin2 $p |- ( F Fn A -> ( F |` ( B i^i A ) ) Fn ( B i^i A ) ) $=
    ( cin wss wfn cres wi inss2 ssfnres ax-mp ) BADZAECAFCLGLFHBAILACJK $.
    $( [9-Aug-94] $)

  $( Functionality and domain of restricted identity. $)
  fnresi $p |- ( I |` A ) Fn A $=
    ( cid cres wfn wfun cdm wceq wa funi funres ax-mp wss cvv ssv dmi sseqtr4
    ssdmres mpbi pm3.2i df-fn mpbir ) BACZADUBEZUBFAGZHUCUDBEUCIABJKABFZLUDAMUE
    ANOPABQRSUBATUA $.
    $( [28-Aug-04] $) $( [27-Aug-04] $)

  ${
    $d x y F $.  $( `x y` are dummy. $)
    $( A function with empty domain is empty. $)
    fn0 $p |- ( F Fn (/) <-> F = (/) ) $=
      ( vx vy c0 wfn wceq cv cop wcel wb wal cdm wn wa fndm wex noel eleq2
      mtbiri visset eldm2 bineg sylib alnex sylibr 19.21bi jctir pm5.21 3syl
      19.21aivv wrel fnrel rel0 cleqrel syl mpbird wfun fun0 dm0 pm3.2i df-fn
      mpbir fneq1 mpbiri impbi ) ADEZADFZVFVGBGZCGHZAIZVIDIZJZCKBKZVFVLBCVFALZD
      FZVJMZVKMZNVLDAOVOVPVQVOVPCVOVJCPZMZVPCKVOVHVNIZMVSVOVTVHDIVHQVNDVHRSVTVR
      CVHABTUAUBUCVJCUDUEUFVIQUGVJVKUHUIUJVFAUKZDUKZNVGVMJVFWAWBDAULUMUGBCADUNU
      OUPVGVFDDEZWCDUQZDLDFZNWDWEURUSUTDDVAVBDADVCVDVE $.
      $( [15-Apr-98] $)
  $}

  $( If the domain of a function is a set, the function is a set.  Theorem
     6.16(1) of [TakeutiZaring] p. 28.  This theorem is derived using the
     Axiom of Replacement in the form of ~ funimaexg . $)
  fnex $p |- ( A e. B -> ( F Fn A -> F e. V ) ) $=
    ( wfn wcel cvv cdm crn cxp wa fndm eleq1d biimprd cima wfun funimaexg
    fnfun syl5 com12 wceq imaeq2 syl imadmrn syl5eqr sylibrd jcad xpexg syl6
    wss ssexg wrel fnrel relssdr syld ) CADZABEZCFEZUOUPCGZCHZIZFEZUQUOUPURBEZU
    SFEZJVAUOUPVBVCUOVBUPUOURABACKZLMUOUPCANZFEZVCUPUOVFUPCOVFUOCABPACQRSUOUSVE
    FUOCURNZVEUSUOURATVGVETVDURACUAUBCUCUDLUEUFURUSBFUGUHVAUOUQVACUTUIZUQUOCUTF
    UJUOCUKVHACULCUMUBRSUNS $.
    $( [14-Aug-94] $)

  $( If the domain of a function exists, so the function.  Part of Theorem
     4.15(v) of [Monk1] p. 46.  This theorem is derived using the Axiom of
     Replacement in the form of ~ fnex .  (Note:  Any resemblance between
     F.U.N.E.X. and "Have You Any Eggs" is purely a coincidence originated by
     Swedish chefs.) $)
  funex $p |- ( dom F e. B -> ( Fun F -> F e. V ) ) $=
    ( cdm wcel wfn cvv wfun fnex wceq df-fn cleqid mpbiranr syl5ibr ) BCZADBNEZ
    BFDBGZNABHOPNNIBNJNKLM $.
    $( [11-Nov-95] $)

  ${
    $d x y A $.
    funopabex.1 $e |- A e. V $.
    funopabex.2 $e |- ( x e. A -> E* y ph ) $.
    $( Existence of a function expressed as class of ordered pairs. $)
    funopabex $p |- { <. x , y >. | ( x e. A /\ ph ) } e. V $=
      ( cv wcel wa copab cdm cvv wfun dmopabss ssexi wmo funopab wi moanimv
      mpbir mpgbir funex mp2 ) BGDHZAIZBCJZKZLHUFMZUFLHUGDEABCDNOUHUECPZBUEBCQU
      IUDACPRFUDACSTUALUFUBUC $.
      $( [21-Jul-96] $)
  $}

  $( If the domain of a function exists, so does its range.  Part of Theorem
     4.15(v) of [Monk1] p. 46.  This theorem is derived using the Axiom of
     Replacement in the form of ~ funex . $)
  funrnex $p |- ( dom F e. B -> ( Fun F -> ran F e. V ) ) $=
    ( cdm wcel wfun cvv crn funex rnexg syl6 ) BCADBEBFDBGFDABHBFIJ $.
    $( [11-Nov-95] $)

  ${
    $d ph z w v $. $d x y z w v $.
    $( A version of the Axiom of Replacement.  Normally ` ph ` would have free
       variables ` x ` and ` y ` .  Axiom 6 of [Kunen] p. 12.  The Separation
       Scheme ~ zfaus cannot be derived from this version and must be stated as
       a separate axiom in an axiom system (such as Kunen's) that uses this
       version in place of our ~ ax-rep . $)
    zfrep6 $p |- ( A. x e. z E! y ph -> E. w A. x e. z E. y e. w ph ) $=
      ( vv wel wa copab crn cvv wcel wrex cv wral wex weu ax-17 wceq hbopab1
      hbrn hbeleq hbopab2 rexeqf birald cla4egf cdm wfun funrnex visset crab
      euex r19.20si rabid2 sylibr cab 19.42v biabi dmopab df-rab 3eqtr4
      syl6reqr eleq1d mpbiri wi wal wmo eumo syl3 moanimv 19.20i df-ral
      funopab 3imtr4 syl13 hbra1 eleq2d cop opabid opelrn sylbir exp impac
      19.22i cleqabi df-rex syl6bir r19.21ai ) BDGZAHZBCIZJZKLZACWLMZBDNZOZACEN
      ZMZBWOOZEPACQZBWOOZWSWPEFWLKFNWLLERWPERWQWLSWRWNBWOBEWLBEWKWJBCETUAUBACFW
      QWLFEGCRCFWKWJBCFUCUAUDUEUFWKUGZKLZWKUHZWMXAKWKUIXAXCWOKLDUJXAXBWOKXAWOAC
      PZBWOUKZXBXAXEBWOOWOXFSWTXEBWOACULUMXEBWOUNUOWJCPZBUPWIXEHZBUPXBXFXGXHBWI
      ACUQURWJBCUSZXEBWOUTVAVBZVCVDWIWTVEZBVFWJCVGZBVFXAXDXKXLBXKWIACVGZVEXLWTX
      MWIACVHVIWIACVJUOVKWTBWOVLWJBCVMVNVOXAWNBWOWTBWOVPXAWIBNZXBLZWNXAXBWOXNXJ
      VQXGCNZWLLZAHZCPXOWNWJXRCWIAXQWIAXQWJXNXPVRWKLXQWJBCVSXNXPWKBUJCUJVTWAWBW
      CWDXGBXBXIWEACWLWFVNWGWHVO $.
      $( [10-Oct-03] $) $( [10-Oct-03] $)
  $}

  ${
    $d x y z A $.  $d z F $.  $d x y z w $.
    fnopabg.1 $e |- F = { <. x , y >. | ( x e. A /\ ph ) } $.
    $( Functionality and domain of an ordered pair abstraction. $)
    fnopabg $p |- ( A. x e. A E! y ph <-> F Fn A ) $=
      ( vz vw weu wral wfn cv wcel wa copab wfun cdm wceq wmo wal hbra1 wi ra4
      eumo syl3 moanimv sylibr syl 19.21ai funopab wex euex r19.20si dmopab2
      sylib jca df-fn wb fneq1 ax-mp hbopab1 eleq2i bial 3imtr4 ax-17 hbfn cop
      fneu2 hbopab2 hbel weq opeq2 eleq1d cbveu opabid bitr bieu euanv
      pm3.27bd exp r19.21ai impbi ) ACIZBDJZEDKZWDBLZDMZANZBCOZDKZWEWDWIPZWIQDR
      ZNWJWDWKWLWDWHCSZBTWKWDWMBWCBDUAWDWGWCUBZWMWCBDUCWNWGACSZUBWMWCWOWGACUDUE
      WGACUFUGUHUIWHBCUJUGWDACUKZBDJWLWCWPBDACULUMABCDUNUOUPWIDUQUGEWIRWEWJURFD
      EWIUSUTUGWEWCBDBGDEGLZWIMZWRBTWQEMZWSBTWHBCGVAEWIWQFVBZWSWRBWTVCVDWQDMBVE
      VFWEWGWCWEWGNZWFCLZVGZEMZCIZWCXAWFWQVGZEMZGIXEGDWFEVHXGXDGCCHGXFEHLXFMCVE
      WRWRCTWSWSCTWHBCGVIWTWSWRCWTVCVDVJXDGVEGCVKXFXCEWQXBWFVLVMVNUOXEWGWCXEWHC
      IWGWCNXDWHCXDXCWIMWHEWIXCFVBWHBCVOVPVQWGACVRVPVSUHVTWAWB $.
      $( [31-Jan-04] $) $( [30-Jan-04] $)
  $}

  ${
    $d x y A $.
    fnopab.1 $e |- ( x e. A -> E! y ph ) $.
    fnopab.2 $e |- F = { <. x , y >. | ( x e. A /\ ph ) } $.
    $( Functionality and domain of an ordered pair abstraction. $)
    fnopab $p |- F Fn A $=
      ( weu wral wfn rgen fnopabg mpbi ) ACHZBDIEDJNBDFKABCDEGLM $.
      $( [5-Mar-96] $)
  $}

  ${
    $d x y A $.  $d y B $.
    fnopab2.1 $e |- B e. V $.
    fnopab2.2 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    $( Functionality and domain of an ordered pair abstraction. $)
    fnopab2 $p |- F Fn A $=
      ( cv wceq weu wcel eueq1 a1i fnopab ) BHDIZABCEOBJAHCKBDFLMGN $.
      $( [24-Feb-04] $) $( [29-Jan-04] $)
  $}

  $( Equality theorem for functions. $)
  feq1 $p |- ( F = G -> ( F : A --> B <-> G : A --> B ) ) $=
    ( wceq wfn crn wss wa wf fneq1 rneq sseq1d bi2and df-f 3bitr4g ) CDEZCAFZCG
    ZBHZIDAFZDGZBHZIABCJABDJQRUATUCACDKQSUBBCDLMNABCOABDOP $.
    $( [1-Aug-94] $)

  $( Equality theorem for functions. $)
  feq2 $p |- ( A = B -> ( F : A --> C <-> F : B --> C ) ) $=
    ( wceq wfn crn wss wa wf fneq2 birand df-f 3bitr4g ) ABEZDAFZDGCHZIDBFZQIAC
    DJBCDJOPRQABDKLACDMBCDMN $.
    $( [1-Aug-94] $)

  $( Equality theorem for functions. $)
  feq3 $p |- ( A = B -> ( F : C --> A <-> F : C --> B ) ) $=
    ( wceq wfn crn wss wa wf sseq2 biland df-f 3bitr4g ) ABEZDCFZDGZAHZIPQBHZIC
    ADJCBDJORSPABQKLCADMCBDMN $.
    $( [1-Aug-94] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a mapping. $)
    hbf $p |- ( F : A --> B -> A. x F : A --> B ) $=
      ( wfn crn wss wa wal wf hbfn hbrn hbss hban df-f bial 3imtr4 ) ECIZEJZDKZ
      LZUEAMCDENZUFAMUBUDAABCEFGOABUCDABEFPHQRCDESZUFUEAUGTUA $.
      $( [2-Feb-04] $) $( [29-Jan-04] $)
  $}

  $( A mapping is a function. $)
  ffn $p |- ( F : A --> B -> F Fn A ) $=
    ( wf wfn crn wss df-f pm3.26bd ) ABCDCAECFBGABCHI $.
    $( [2-Aug-94] $)

  $( Any function is a mapping into ` V ` . $)
  fnf $p |- ( F Fn A <-> F : A --> V ) $=
    ( wfn cvv wf crn wss ssv wa df-f biimpr mpan2 ffn impbi ) BACZADBEZOBFZDGZP
    QHPORIADBJKLADBMN $.
    $( [31-Oct-95] $)

  $( A mapping is a function. $)
  ffun $p |- ( F : A --> B -> Fun F ) $=
    ( wf wfn wfun ffn fnfun syl ) ABCDCAECFABCGACHI $.
    $( [3-Aug-94] $)

  $( A mapping is a relation. $)
  frel $p |- ( F : A --> B -> Rel F ) $=
    ( wf wfn wrel ffn fnrel syl ) ABCDCAECFABCGACHI $.
    $( [3-Aug-94] $)

  $( The domain of a mapping. $)
  fdm $p |- ( F : A --> B -> dom F = A ) $=
    ( wf wfn cdm wceq ffn fndm syl ) ABCDCAECFAGABCHACIJ $.
    $( [2-Aug-94] $)

  $( The range of a mapping. $)
  frn $p |- ( F : A --> B -> ran F (_ B ) $=
    ( wf wfn crn wss df-f pm3.27bd ) ABCDCAECFBGABCHI $.
    $( [3-Aug-94] $)

  $( A function maps to its range. $)
  fnfrn $p |- ( F Fn A <-> F : A --> ran F ) $=
    ( wfn crn wss wa wf ssid biantru df-f bitr4 ) BACZLBDZMEZFAMBGNLMHIAMBJK $.
    $( [1-Sep-99] $)

  $( Expanding the co-domain of a mapping. $)
  fss $p |- ( ( F : A --> B /\ B (_ C ) -> F : A --> C ) $=
    ( wf wss wa wfn crn ffn adantr wi frn sstr syl imp jca df-f sylibr ) ABDEZB
    CFZGZDAHZDIZCFZGACDEUBUCUETUCUAABDJKTUAUETUDBFUAUELABDMUDBCNOPQACDRS $.
    $( [10-May-98] $)

  $( Composition of two mappings. $)
  fco $p |- ( ( F : B --> C /\ G : A --> B ) -> ( F o. G ) : A --> C ) $=
    ( wf wa ccom wfn crn wss wfun cdm wceq funco ffun syl2an fdm sseq2d frn
    syl5bir imp dmcosseq syl adantl eqtrd jca df-fn sylibr wi rnco sstr syl5
    ax-mp adantr df-f ) BCDFZABEFZGZDEHZAIZUTJZCKZGACUTFUSVAVCUSUTLZUTMZANZGVAU
    SVDVFDLELVDUQURDEOBCDPABEPQUSVEEMZAUSEJZDMZKZVEVGNUQURVJUQVJVHBKURUQVIBVHBC
    DRSABETUAUBDEUCUDURVGANUQABERUEUFUGUTAUHUIUQVCURVBDJZKZUQVCUJDEUKVLVKCKVCUQ
    VBVKCULBCDTUMUNUOUGACUTUPUI $.
    $( [29-Aug-99] $)

  $( A mapping is a class of ordered pairs. $)
  fssxp $p |- ( F : A --> B -> F (_ ( A X. B ) ) $=
    ( wf cdm crn cxp wss frn ssid ssxp mpan syl wceq wb fdm xpeq1 sseq1 3syl
    mpbird wrel wi frel relssdr sstr mpd ) ABCDZCEZCFZGZABGZHZCUKHZUGULAUIGZUKH
    ZUGUIBHZUOABCIAAHUPUOAJAAUIBKLMUGUHANUJUNNULUOOABCPUHAUIQUJUNUKRSTUGCUACUJH
    ULUMUBABCUCCUDCUJUKUESUF $.
    $( [3-Aug-94] $)

  ${
    opelf.1 $e |- D e. V $.
    $( The members of an ordered pair element of a mapping belong to the
       mapping's domain and codomain. $)
    opelf $p |- ( ( F : A --> B /\ <. C , D >. e. F ) ->
                ( C e. A /\ D e. B ) ) $=
      ( wf cop wcel wa cxp fssxp sseld opelxp syl6ib imp ) ABEGZCDHZEIZCAIDBIJZ
      QSRABKZITQEUARABELMCDABFNOP $.
      $( [11-Dec-03] $) $( [10-Dec-03] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( Composition of a mapping and restricted identity. $)
    fcoi1 $p |- ( F : A --> B -> ( F o. ( I |` A ) ) = F ) $=
      ( vx vy vz wf wfn cid cres ccom wceq ffn cv cop wcel wb wal wa wi visset
      fnop exp pm4.71r sylib wex weq cvv opelcog mp2an opelres wbr ideq df-br
      cleqcom 3bitr3 biran bitr anass biex opeq1 eleq1d biland ceqsexv 3bitr
      syl6rbbr 19.21aivv wrel fnrel relco cleqrel mpan syl mpbird ) ABCGCAHZCIA
      JZKZCLZABCMVOVRDNZENZOZVQPZWACPZQZERDRZVOWDDEVOWCVSAPZWCSZWBVOWCWFTWCWGQV
      OWCWFAVSVTCDUAZEUAZUBUCWCWFUDUEWBVSFNZOZVPPZWJVTOZCPZSZFUFZFDUGZWFWNSZSZF
      UFWGVSUHPVTUHPWBWPQWHWIFVSVTCVPUHUHUIUJWOWSFWOWQWFSZWNSWSWLWTWNWLWKIPZWFS
      WTVSWJIAFUAZUKXAWQWFVSWJIULDFUGXAWQVSWJWHXBUMVSWJIUNVSWJUOUPUQURUQWQWFWNU
      SURUTWRWGFVSWHWQWNWCWFWQWMWACWJVSVTVAVBVCVDVEVFVGVOCVHZVRWEQZACVIVQVHXCXD
      CVPVJDEVQCVKVLVMVNVM $.
      $( [24-Dec-03] $) $( [13-Dec-03] $)

    $( Composition of restricted identity and a mapping. $)
    fcoi2 $p |- ( F : A --> B -> ( ( I |` B ) o. F ) = F ) $=
      ( vx vy vz wf cid cres ccom wceq cv cop wcel wb wal wa wi visset opelf
      pm3.27d exp pm4.71 sylib wex weq cvv opelcog mp2an opelres wbr df-br
      ideq bitr3 biran bitr bilan an12 biex opeq2 eleq1d eleq1 bi2and ceqsexv
      3bitr syl6rbbr 19.21aivv wrel frel relco cleqrel mpan syl mpbird ) ABCGZH
      BIZCJZCKZDLZELZMZVQNZWACNZOZEPDPZVOWDDEVOWCWCVTBNZQZWBVOWCWFRWCWGOVOWCWFV
      OWCQVSANWFABVSVTCESZTUAUBWCWFUCUDWBVSFLZMZCNZWIVTMZVPNZQZFUEZFEUFZWKWIBNZ
      QZQZFUEWGVSUGNVTUGNWBWOODSWHFVSVTVPCUGUGUHUIWNWSFWNWKWPWQQZQWSWMWTWKWMWLH
      NZWQQWTWIVTHBWHUJXAWPWQXAWIVTHUKWPWIVTHULWIVTFSWHUMUNUOUPUQWKWPWQURUPUSWR
      WGFVTWHWPWKWCWQWFWPWJWACWIVTVSUTVAWIVTBVBVCVDVEVFVGVOCVHZVRWEOZABCVIVQVHX
      BXCVPCVJDEVQCVKVLVMVN $.
      $( [16-May-04] $) $( [13-Dec-03] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.  $d y C $.
    $( There is exactly one value of a function in its codomain. $)
    feu $p |- ( ( F : A --> B /\ C e. A ) -> E! y e. B <. C , y >. e. F ) $=
      ( wf wcel wa cv cop weu wreu wfn fneu2 ffn sylan wb wi visset opelf
      pm3.27d exp pm4.71r sylib bieudv adantr mpbid df-reu sylibr ) BCEFZDBGZHZ
      AIZCGZDUMJEGZHZAKZUOACLULUOAKZUQEBMUKURUJABDENBCEOPUJURUQQUKUJUOUPAUJUOUN
      RUOUPQUJUOUNUJUOHUKUNBCDUMEASTUAUBUOUNUCUDUEUFUGUOACUHUI $.
      $( [12-Dec-03] $) $( [10-Dec-03] $)
  $}

  ${
    $d x y F $.  $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( The converse of a restriction of a function. $)
    fcnvres $p |- ( F : A --> B -> `' ( F |` A ) = ( `' F |` B ) ) $=
      ( vy vx wf cv cop cres ccnv wcel wb wal wceq wa wi visset opelf pm3.26d
      exp pm4.71 sylib opelcnv opelres bitr syl6bbr pm3.27d biran bitr3d
      19.21aivv wrel relcnv relres cleqrel mp2an sylibr ) ABCFZDGZEGZHZCAIZJZKZ
      UTCJZBIZKZLZEMDMZVBVENZUQVGDEUQUSURHZCKZVCVFUQVKVKUSAKZOZVCUQVKVLPVKVMLUQ
      VKVLUQVKOZVLURBKZABUSURCDQZRZSTVKVLUAUBVCVJVAKVMURUSVAVPEQZUCUSURCAVPUDUE
      UFUQVKVKVOOZVFUQVKVOPVKVSLUQVKVOVNVLVOVQUGTVKVOUAUBVFUTVDKZVOOVSURUSVDBVR
      UDVTVKVOURUSCVPVRUCUHUEUFUIUJVBUKVEUKVIVHLVAULVDBUMDEVBVEUNUOUP $.
      $( [26-Mar-98] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x F $.
    fint.1 $e |- -. B = (/) $.
    $( Function into an intersection. $)
    fint $p |- ( F : A --> |^| B <-> A. x e. B F : A --> x ) $=
      ( wfn crn cint wss wa cv wral wf c0 wceq wn wb r19.3rzv ax-mp ssint
      bi2an r19.26 bitr4 df-f biral 3bitr4 ) DBFZDGZCHZIZJZUGUHAKZIZJZACLZBUIDM
      BULDMZACLUKUGACLZUMACLZJUOUGUQUJURCNOPUGUQQEUGACRSAUHCTUAUGUMACUBUCBUIDUD
      UPUNACBULDUDUEUF $.
      $( [14-Oct-99] $)
  $}

  $( Function into an intersection. $)
  fin $p |- ( F : A --> ( B i^i C ) <-> ( F : A --> B /\ F : A --> C ) ) $=
    ( wfn crn cin wss wa wf anidm ssin bi2an an4 bitr3 df-f 3bitr4 ) DAEZDFZBCG
    ZHZIZRSBHZIZRSCHZIZIZATDJABDJZACDJZIUBRRIZUCUEIZIUGUJRUKUARKSBCLMRRUCUENOAT
    DPUHUDUIUFABDPACDPMQ $.
    $( [14-Sep-99] $)

  $( If the domain of a function is a set, the function is a set. $)
  fex $p |- ( A e. C -> ( F : A --> B -> F e. V ) ) $=
    ( wcel wfn cvv wf fnex ffn syl5 ) ACEDAFDGEABDHACDIABDJK $.
    $( [3-Oct-99] $)

  $( The empty function. $)
  f0 $p |- (/) : (/) --> A $=
    ( c0 wf wfn crn wss wa wceq cleqid fn0 mpbir rn0 0ss eqsstr pm3.2i df-f )
    BABCBBDZBEZAFZGQSQBBHBIBJKRBALAMNOBABPK $.
    $( [14-Aug-99] $)

  $( A class is a function with empty codomain iff it and its domain are
     empty. $)
  f00 $p |- ( F : A --> (/) <-> ( F = (/) /\ A = (/) ) ) $=
    ( c0 wf wceq wa wfn wfun cdm ffun crn wss frn ss0 syl dm0rn0 sylibr jca
    df-fn fn0 sylib fdm eqtr3d f0 feq1 feq2 sylan9bb mpbiri impbi ) ACBDZBCEZAC
    EZFZUJUKULUJBCGZUKUJBHZBIZCEZFUNUJUOUQACBJUJBKZCEZUQUJURCLUSACBMURNOBPQZRBC
    SQBTUAUJUPACACBUBUTUCRUMUJCCCDZCUDUKUJACCDULVAACBCUEACCCUFUGUHUI $.
    $( [27-Dec-03] $) $( [10-Dec-03] $)

  ${
    $d x y A $.  $d x y B $.
    fconst.1 $e |- B e. V $.
    $( A constant function. $)
    fconst $p |- ( A X. { B } ) : A --> { B } $=
      ( vx vy c0 wceq csn cxp wf f0 wb xpeq1 xp0 syl6eq feq1 syl feq2 bitrd
      mpbiri wn wfn crn wss wa cdm dmxp ccnv df-rn cnvxp dmeqi eqtr syl5eq
      eqimss wfun wrel cv cop wcel wmo wal relxp moeq moani visset opelxp elsn
      bilan bitr bimo mpbir ax-gen pm3.2i dffunmoOLD snnz ax-mp df-fn jctil
      df-f sylibr pm2.61i ) AFGZABHZAWCIZJZWBWEFWCFJZWCKWBWEAWCFJZWFWBWDFGWEWGL
      WBWDFWCIFAFWCMWCNOAWCWDFPQAFWCFRSTWBUAZWDAUBZWDUCZWCUDZUEWEWHWKWIWHWJWCGW
      KWHWCAIZUFZWCWJWCAUGWJWDUHZUFWMWDUIWNWLAWCUJUKULUMWJWCUNQWIWDUOZWDUFAGZUE
      WOWPWOWDUPZDUQZEUQZURWDUSZEUTZDVAZUEWQXBAWCVBXADXAWRAUSZWSBGZUEZEUTXDXCEE
      BVCVDWTXEEWTXCWSWCUSZUEXEWRWSAWCEVEVFXFXDXCEBVGVHVIVJVKVLVMDEWDVNVKWCFGUA
      WPBCVOAWCUGVPVMWDAVQVKVRAWCWDVSVTWA $.
      $( [14-Aug-99] $)
  $}

  $( Equality theorem for one-to-one functions. $)
  f1eq1 $p |- ( F = G -> ( F : A -1-1-> B <-> G : A -1-1-> B ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq1 wb cnveq funeq syl bi2and df-f1 3bitr4g )
    CDEZABCFZCGZHZIABDFZDGZHZIABCJABDJSTUCUBUEABCDKSUAUDEUBUELCDMUAUDNOPABCQABD
    QR $.
    $( [10-Feb-97] $)

  $( Equality theorem for one-to-one functions. $)
  f1eq2 $p |- ( A = B -> ( F : A -1-1-> C <-> F : B -1-1-> C ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq2 birand df-f1 3bitr4g ) ABEZACDFZDGHZIBCDFZQ
    IACDJBCDJOPRQABCDKLACDMBCDMN $.
    $( [10-Feb-97] $)

  $( Equality theorem for one-to-one functions. $)
  f1eq3 $p |- ( A = B -> ( F : C -1-1-> A <-> F : C -1-1-> B ) ) $=
    ( wceq wf ccnv wfun wa wf1 feq3 birand df-f1 3bitr4g ) ABEZCADFZDGHZICBDFZQ
    ICADJCBDJOPRQABCDKLCADMCBDMN $.
    $( [10-Feb-97] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf1.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf1.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf1.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a one-to-one function. $)
    hbf1 $p |- ( F : A -1-1-> B -> A. x F : A -1-1-> B ) $=
      ( wf ccnv wfun wa wal wf1 hbf hbcnv hbfun hban df-f1 bial 3imtr4 ) CDEIZE
      JZKZLZUEAMCDENZUFAMUBUDAABCDEFGHOABUCABEFPQRCDESZUFUEAUGTUA $.
      $( [16-May-04] $) $( [16-May-04] $)
  $}

  ${
    $d x y F $.
    $( Alternate definition of a one-to-one function. $)
    f11 $p |- ( F : A -1-1-> B <-> ( F : A --> B /\
                    A. y E* x <. x , y >. e. F ) ) $=
      ( wf1 wf ccnv wfun wa cv cop wcel wmo wal df-f1 wrel dffunmoOLD relcnv
      mpbiran visset opelcnv bimo bial bitr bilan ) CDEFCDEGZEHZIZJUGAKZBKZLEMZ
      ANZBOZJCDEPUIUNUGUIUKUJLUHMZANZBOZUNUIUHQUQBAUHRESTUPUMBUOULAUKUJEBUAAUAU
      BUCUDUEUFUE $.
      $( [31-Dec-96] $)
  $}

  $( A one-to-one mapping is a mapping. $)
  f1f $p |- ( F : A -1-1-> B -> F : A --> B ) $=
    ( wf1 wf ccnv wfun df-f1 pm3.26bd ) ABCDABCECFGABCHI $.
    $( [31-Dec-96] $)

  $( Two ways of expressing that a set ` A ` (not necessarily a function) is
     one-to-one.  Each side is equivalent to Definition 6.4(3) of
     [TakeutiZaring] p. 24, who use the notation "Un_2 (A)" for one-to-one.
     We do not introduce a separate notation since we do not use it very
     often. $)
  f1cnv $p |- ( `' `' A : dom A -1-1-> V <-> ( Fun `' A /\ Fun `' `' A ) ) $=
    ( cdm cvv ccnv wf1 wf wfun wa df-f1 wfn fnf wceq df-fn crn dfdm4 df-rn
    eqtr2 mpbiranr bitr3 wb wrel relcnv dfrel2 mpbi funeq ax-mp bi2an ancom
    3bitr ) ABZCADZDZEUJCULFZULDZGZHULGZUKGZHUQUPHUJCULIUMUPUOUQUMULUJJZUPUJULK
    URUPULBZUJLULUJMUJUKNUSAOUKPQRSUNUKLZUOUQTUKUAUTAUBUKUCUDUNUKUEUFUGUPUQUHUI
    $.
    $( [?] $) $( [13-Aug-04] $)

  $( Composition of one-to-one functions.  Exercise 30 of [TakeutiZaring]
     p. 25. $)
  f1co $p |- ( ( F : B -1-1-> C /\ G : A -1-1-> B ) ->
              ( F o. G ) : A -1-1-> C ) $=
    ( wf ccnv wfun wa ccom wf1 fco funco wceq wb cnvco funeq ax-mp sylibr
    ancoms im2an an4s df-f1 bi2an 3imtr4 ) BCDFZDGZHZIZABEFZEGZHZIZIACDEJZFZUNG
    ZHZIZBCDKZABEKZIACUNKUFUJUHULURUFUJIUOUHULIUQABCDELULUHUQULUHIUKUGJZHZUQUKU
    GMUPVANUQVBODEPUPVAQRSTUAUBUSUIUTUMBCDUCABEUCUDACUNUCUE $.
    $( [28-May-98] $)

  $( Equality theorem for onto functions. $)
  foeq1 $p |- ( F = G -> ( F : A -onto-> B <-> G : A -onto-> B ) ) $=
    ( wceq wfn crn wa wfo fneq1 rneq cleq1d bi2and df-fo 3bitr4g ) CDEZCAFZCGZB
    EZHDAFZDGZBEZHABCIABDIPQTSUBACDJPRUABCDKLMABCNABDNO $.
    $( [1-Aug-94] $)

  $( Equality theorem for onto functions. $)
  foeq2 $p |- ( A = B -> ( F : A -onto-> C <-> F : B -onto-> C ) ) $=
    ( wceq wfn crn wa wfo fneq2 birand df-fo 3bitr4g ) ABEZDAFZDGCEZHDBFZPHACDI
    BCDINOQPABDJKACDLBCDLM $.
    $( [1-Aug-94] $)

  $( Equality theorem for onto functions. $)
  foeq3 $p |- ( A = B -> ( F : C -onto-> A <-> F : C -onto-> B ) ) $=
    ( wceq wfn crn wa wfo cleq2 biland df-fo 3bitr4g ) ABEZDCFZDGZAEZHOPBEZHCAD
    ICBDINQROABPJKCADLCBDLM $.
    $( [1-Aug-94] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbfo.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfo.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbfo.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for an onto function. $)
    hbfo $p |- ( F : A -onto-> B -> A. x F : A -onto-> B ) $=
      ( wfn crn wceq wa wal wfo hbfn hbrn hbeq hban df-fo bial 3imtr4 ) ECIZEJZ
      DKZLZUEAMCDENZUFAMUBUDAABCEFGOABBUCDABEFPHQRCDESZUFUEAUGTUA $.
      $( [16-May-04] $) $( [16-May-04] $)
  $}

  $( An onto mapping is a mapping. $)
  fof $p |- ( F : A -onto-> B -> F : A --> B ) $=
    ( wfn crn wceq wa wss wfo wf eqimss imlan df-fo df-f 3imtr4 ) CADZCEZBFZGPQ
    BHZGABCIABCJRSPQBKLABCMABCNO $.
    $( [3-Aug-94] $)

  $( The codomain of an onto function is its range. $)
  forn $p |- ( F : A -onto-> B -> ran F = B ) $=
    ( wfo wfn crn wceq df-fo pm3.27bd ) ABCDCAECFBGABCHI $.
    $( [3-Aug-94] $)

  $( The image of the domain of an onto function. $)
  foima $p |- ( F : A -onto-> B -> ( F " A ) = B ) $=
    ( wfo cima crn cdm wf wceq fof fdm imaeq2 3syl imadmrn syl5reqr forn eqtrd
    ) ABCDZCAEZCFZBRCCGZEZSTRABCHUAAIUBSIABCJABCKUAACLMCNOABCPQ $.
    $( [29-Nov-02] $)

  $( A function maps onto its range. $)
  fnforn $p |- ( F Fn A <-> F : A -onto-> ran F ) $=
    ( wfn crn wceq wa wfo cleqid biantru df-fo bitr4 ) BACZLBDZMEZFAMBGNLMHIAMB
    JK $.
    $( [10-May-98] $)

  $( A function maps its domain onto its range. $)
  funforn $p |- ( Fun A <-> A : dom A -onto-> ran A ) $=
    ( wfun cdm wfn crn wfo wceq wa cleqid biantru df-fn bitr4 fnforn bitr ) ABZ
    AACZDZPAEAFOOPPGZHQROPIJAPKLPAMN $.
    $( [30-Jul-04] $) $( [23-Jul-04] $)

  $( If the domain of an onto function exists, so does its codomain. $)
  fornex $p |- ( A e. C -> ( F : A -onto-> B -> B e. V ) ) $=
    ( wfo wcel cvv cdm crn wf wfun wi fof ffun funrnex com12 3syl wceq fdm syl
    eleq1d forn 3imtr3d ) ABDEZACFZBGFZUDDHZCFZDIZGFZUEUFUDABDJZDKZUHUJLABDMZAB
    DNUHULUJCDOPQUDUGACUDUKUGARUMABDSTUAUDUIBGABDUBUAUCP $.
    $( [30-Jul-04] $) $( [23-Jul-04] $)

  $( Restriction of a function. $)
  fores $p |- ( ( Fun F /\ A (_ dom F ) ->
              ( F |` A ) :  A -onto-> ( F " A ) ) $=
    ( wfun cdm wss wa cres cima wfo funres imran wfn wceq df-fn crn df-fo
    df-ima cleqcomi mpbiranr ssdmresOLD cleqcom bitr bilan 3bitr4 sylibr ) BCZA
    BDEZFBAGZCZUGFZABAHZUHIZUFUIUGABJKUHALZUIUHDZAMZFULUJUHANULUMUHOZUKMAUKUHPU
    KUPBAQRSUGUOUIUGAUNMUOABTAUNUAUBUCUDUE $.
    $( [4-Mar-97] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq1 $p |- ( F = G -> ( F : A -1-1-onto-> B <-> G : A -1-1-onto-> B ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq1 foeq1 bi2and df-f1o 3bitr4g ) CDEZABCFZABCGZHA
    BDFZABDGZHABCIABDIOPRQSABCDJABCDKLABCMABDMN $.
    $( [10-Feb-97] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq2 $p |- ( A = B -> ( F : A -1-1-onto-> C <-> F : B -1-1-onto-> C ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq2 foeq2 bi2and df-f1o 3bitr4g ) ABEZACDFZACDGZHB
    CDFZBCDGZHACDIBCDIOPRQSABCDJABCDKLACDMBCDMN $.
    $( [10-Feb-97] $)

  $( Equality theorem for one-to-one onto functions. $)
  f1oeq3 $p |- ( A = B -> ( F : C -1-1-onto-> A <-> F : C -1-1-onto-> B ) ) $=
    ( wceq wf1 wfo wa wf1o f1eq3 foeq3 bi2and df-f1o 3bitr4g ) ABEZCADFZCADGZHC
    BDFZCBDGZHCADICBDIOPRQSABCDJABCDKLCADMCBDMN $.
    $( [10-Feb-97] $)

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbf1o.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbf1o.2 $e |- ( y e. A -> A. x y e. A ) $.
    hbf1o.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for a one-to-one onto function. $)
    hbf1o $p |- ( F : A -1-1-onto-> B -> A. x F : A -1-1-onto-> B ) $=
      ( wf1 wfo wa wal wf1o hbf1 hbfo hban df-f1o bial 3imtr4 ) CDEIZCDEJZKZUBA
      LCDEMZUCALTUAAABCDEFGHNABCDEFGHOPCDEQZUCUBAUDRS $.
      $( [16-May-04] $) $( [16-May-04] $)
  $}

  $( A one-to-one onto mapping is a one-to-one mapping. $)
  f1of1 $p |- ( F : A -1-1-onto-> B -> F : A -1-1-> B ) $=
    ( wf1o wf1 wfo df-f1o pm3.26bd ) ABCDABCEABCFABCGH $.
    $( [17-Dec-03] $) $( [12-Dec-03] $)

  $( A one-to-one onto mapping is a mapping. $)
  f1of $p |- ( F : A -1-1-onto-> B -> F : A --> B ) $=
    ( wf1o wf1 wf f1of1 f1f syl ) ABCDABCEABCFABCGABCHI $.
    $( [12-Dec-03] $) $( [12-Dec-03] $)

  $( A one-to-one onto mapping is function on its domain. $)
  f1ofn $p |- ( F : A -1-1-onto-> B -> F Fn A ) $=
    ( wf1o wf wfn f1of ffn syl ) ABCDABCECAFABCGABCHI $.
    $( [17-Dec-03] $) $( [12-Dec-03] $)

  $( A one-to-one onto mapping is a function. $)
  f1ofun $p |- ( F : A -1-1-onto-> B -> Fun F ) $=
    ( wf1o wfn wfun f1ofn fnfun syl ) ABCDCAECFABCGACHI $.
    $( [17-Dec-03] $) $( [12-Dec-03] $)

  $( A one-to-one onto mapping is a relation. $)
  f1orel $p |- ( F : A -1-1-onto-> B -> Rel F ) $=
    ( wf1o wfun wrel f1ofun funrel syl ) ABCDCECFABCGCHI $.
    $( [6-Jan-04] $) $( [13-Dec-03] $)

  $( Alternate definition of one-to-one onto function. $)
  f1o2 $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ Fun `' F /\ ran F = B ) ) $=
    ( wf1 wfo wa wfn ccnv wfun crn wceq wf1o w3a wf df-f1 pm3.27bd df-fo biimp
    im2an wss eqimss imlan df-f sylibr imran ancoms biimpr adantl jca impbi
    an12 bitr df-f1o 3anass 3bitr4 ) ABCDZABCEZFZCAGZCHIZCJZBKZFFZABCLUSUTVBMUR
    UTUSVBFZFZVCURVEUPUTUQVDUPABCNZUTABCOZPUQVDABCQZRSVEUPUQVDUTUPVDUTFVFUTFUPV
    DVFUTVDUSVABTZFVFVBVIUSVABUAUBABCUCUDUEVGUDUFVDUQUTUQVDVHUGUHUIUJUTUSVBUKUL
    ABCUMUSUTVBUNUO $.
    $( [10-Feb-97] $)

  $( Alternate definition of one-to-one onto function. $)
  f1o3 $p |- ( F : A -1-1-onto-> B <-> ( F : A -onto-> B /\ Fun `' F ) ) $=
    ( wf1 wfo wa wfn crn wceq ccnv wfun wf1o wf an23 df-f1 df-fo bi2an wss
    eqimss imlan df-f sylibr pm4.71ri biran 3bitr4 df-f1o ) ABCDZABCEZFZCAGZCHZ
    BIZFZCJKZFZABCLUHUNFABCMZUNFZUMFUPUMFZUNFUIUOUPUNUMNUGUQUHUMABCOABCPZQUMURU
    NUMUPUMUJUKBRZFUPULUTUJUKBSTABCUAUBUCUDUEABCUFUHUMUNUSUDUE $.
    $( [25-Mar-98] $)

  $( A one-to-one onto function is an onto function. $)
  f1ofo $p |- ( F : A -1-1-onto-> B -> F : A -onto-> B ) $=
    ( wf1o wfo ccnv wfun f1o3 pm3.26bd ) ABCDABCECFGABCHI $.
    $( [30-Apr-04] $) $( [28-Apr-04] $)

  $( Alternate definition of one-to-one onto function. $)
  f1o4 $p |- ( F : A -1-1-onto-> B <-> ( F Fn A /\ `' F Fn B ) ) $=
    ( wfn ccnv wfun crn wceq w3a wa wf1o 3anass f1o2 cdm df-fn df-rn cleq1i
    bilan bitr4 3bitr4 ) CADZCEZFZCGZBHZIUAUCUEJZJABCKUAUBBDZJUAUCUELABCMUGUFUA
    UGUCUBNZBHZJUFUBBOUEUIUCUDUHBCPQRSRT $.
    $( [25-Mar-98] $)

  $( Alternate definition of one-to-one onto function. $)
  f1o5 $p |- ( F : A -1-1-onto-> B <-> ( F : A -1-1-> B /\ ran F = B ) ) $=
    ( wf1o wf1 wfo wa wfn crn wceq df-f1o df-fo bilan an12 wf f1f ffn syl
    adantr pm4.71ri bitr4 3bitr ) ABCDABCEZABCFZGUCCAHZCIBJZGZGZUCUFGZABCKUDUGU
    CABCLMUHUEUIGUIUCUEUFNUIUEUCUEUFUCABCOUEABCPABCQRSTUAUB $.
    $( [16-Feb-04] $) $( [10-Dec-03] $)

  $( A one-to-one function maps onto its range. $)
  f1orn $p |- ( F : A -1-1-onto-> ran F <-> ( F Fn A /\ Fun `' F ) ) $=
    ( wfn ccnv wfun crn wceq w3a wa wf1o df-3an f1o2 cleqid biantru 3bitr4 ) BA
    CZBDEZBFZRGZHPQIZSIARBJTPQSKARBLSTRMNO $.
    $( [5-Sep-04] $) $( [13-Aug-04] $)

  $( A one-to-one function maps one-to-one onto its range. $)
  f1f1orn $p |- ( F : A -1-1-> B -> F : A -1-1-onto-> ran F ) $=
    ( wf1 wfn ccnv wfun wa crn wf1o wf f1f ffn syl df-f1 pm3.27bd jca f1orn
    sylibr ) ABCDZCAEZCFGZHACICJTUAUBTABCKZUAABCLABCMNTUCUBABCOPQACRS $.
    $( [5-Sep-04] $) $( [4-Sep-04] $)

  $( The converse of a one-to-one onto function is also one-to-one onto. $)
  f1ocnv $p |- ( F : A -1-1-onto-> B -> `' F : B -1-1-onto-> A ) $=
    ( wfn ccnv wfun crn wceq wa wf1o cdm df-rn cleq1i bilan df-fn bitr4 biimp
    fnfun wb wrel fnrel dfrel2OLD sylib funeq syl mpbid fndm dfdm4 syl5eqr jca
    im2an ancoms w3a f1o2 3anass bitr 3imtr4 ) CADZCEZFZCGZBHZIZIZUSBDZUSEZFZUS
    GZAHZIZIZABCJZBAUSJZVCURVKVCVEURVJVCVEVCUTUSKZBHZIVEVBVOUTVAVNBCLMNUSBOPQUR
    VGVIURCFZVGACRURCVFHZVPVGSURCTVQACUACUBUCCVFUDUEUFURCKAVHACUGCUHUIUJUKULVLU
    RUTVBUMVDABCUNURUTVBUOUPVMVEVGVIUMVKBAUSUNVEVGVIUOUPUQ $.
    $( [11-Feb-97] $)

  $( A relation is a one-to-one onto function iff its converse is a one-to-one
     onto function with domain and range interchanged. $)
  f1ocnvb $p |- ( Rel F ->
                ( F : A -1-1-onto-> B <-> `' F : B -1-1-onto-> A ) ) $=
    ( wrel wf1o ccnv wi f1ocnv a1i wceq wb dfrel2OLD f1oeq1 sylbi syl5bir
    impbid ) CDZABCEZBACFZEZRTGQABCHIQRABSFZEZTQCUAJRUBKCLABCUAMNBASHOP $.
    $( [17-May-04] $) $( [8-Dec-03] $)

  $( The restriction of a one-to-one function maps one-to-one onto the
     image. $)
  f1ores $p |- ( ( F : A -1-1-> B /\ C (_ A ) -> ( F |` C ) : C -1-1-onto->
            ( F " C ) ) $=
    ( wf ccnv wfun wa wss cima cres wfo wf1 wf1o cdm fores ffun adantr fdm
    sseq2d biimpar sylan13 funres11 im2an an1rs df-f1 biran f1o3 3imtr4 ) ABDEZ
    DFGZHZCAIZHCDCJZDCKZLZUOFGZHZABDMZUMHCUNUONUJUMUKURUJUMHZUPUKUQDGZCDOZIZUPU
    TCDPUJVAUMABDQRUJVCUMUJVBACABDSTUAUBCDUCUDUEUSULUMABDUFUGCUNUOUHUI $.
    $( [25-Mar-98] $)

  $( The union of two one-to-one onto functions with disjoint domain and
     range. $)
  f1oun $p |- ( ( ( F : A -1-1-onto-> B /\ G : C -1-1-onto-> D ) /\
           ( ( A i^i C ) = (/) /\ ( B i^i D ) = (/) ) ) ->
           ( F u. G ) : ( A u. C ) -1-1-onto-> ( B u. D ) ) $=
    ( wf1o wa cin c0 wceq cun wfn ccnv wi wfun cdm ineq12 cleq1d biland funun
    syl6bir uneq12 dmun syl5eq a1d jcad df-fn syl6ibr exp3a com12 imp an4s
    syl2anb wb cnvun fneq1 ax-mp bitr im2anan9 f1o4 ) ABEGZCDFGZHZACIZJKZBDIZJK
    ZHZACLZBDLZEFLZGZVDVIVLVJMZVLNZVKMZHZVMEAMZENZBMZHFCMZFNZDMZHVIVQOZVBVCVRWA
    VTWCWDVRWAHVFVNVTWCHVHVPEPZEQZAKZHFPZFQZCKZHVFVNOZVRWAWEWHWGWJWKWEWHHZWGWJH
    ZWKWMWLWKWMWLVFVNWMWLVFHZVLPZVLQZVJKZHVNWMWNWOWQWMWNWLWFWIIZJKZHWOWMWSVFWLW
    MWRVEJWFAWICRSTEFUAUBWMWQWNWMWFWILVJWPWFAWICUCEFUDUEUFUGVLVJUHUIUJUKULUMEAU
    HFCUHUNVSPZVSQZBKZHWBPZWBQZDKZHVHVPOZVTWCWTXCXBXEXFWTXCHZXBXEHZXFXHXGXFXHXG
    VHVPXHXGVHHZVSWBLZPZXJQZVKKZHZVPXHXIXKXMXHXIXGXAXDIZJKZHXKXHXPVHXGXHXOVGJXA
    BXDDRSTVSWBUAUBXHXMXIXHXAXDLVKXLXABXDDUCVSWBUDUEUFUGVPXJVKMZXNVOXJKVPXQUOEF
    UPVKVOXJUQURXJVKUHUSUIUJUKULUMVSBUHWBDUHUNUTUMABEVACDFVAUNVJVKVLVAUIUL $.
    $( [26-Mar-98] $)

  $( Composition of one-to-one onto functions. $)
  f1oco $p |- ( ( F : B -1-1-onto-> C /\ G : A -1-1-onto-> B ) ->
              ( F o. G ) : A -1-1-onto-> C ) $=
    ( wfn ccnv wfun crn wceq w3a wa ccom wf1o an6 cdm funco wb cnvco funeq
    ax-mp sylibr ancoms im2an dmcoeq cleq1d biimpar adantrr rncoeq adantrl jca
    cleq2 cleqcoms biimpac sylan an41r3s 3anass df-fn bi2an an4 bitr biran
    3bitr 3imtr4 sylbi f1o2 ) DBFZDGZHZDIZCJZKZEAFZEGZHZEIZBJZKZLZDEMZAFZVTGZHZ
    VTIZCJZKZBCDNZABENZLACVTNVSVGVMLZVIVOLZVKVQLZKZWFVGVIVKVMVOVQODHZEHZLZWJLZD
    PZBJZEPZAJZLZWKLZLZVTHZWCLZVTPZAJZWELZLZWLWFWPXEXBXHWOXDWJWCDEQVOVIWCVOVILV
    NVHMZHZWCVNVHQWBXJJWCXKRDESWBXJTUAUBUCUDWRVQWTVKXHWQVPJZWTVKLZXHWRVQLXLXMLX
    GWEXLWTXGVKXLXGWTXLXFWSADEUEUFUGUHXLVKWEWTXLWEVKXLWDVJCDEUIUFUGUJUKVQWRXLWR
    XLRBVPBVPWQULUMUNUOUPUDWLWIWJWKLZLWOXALZXNLXCWIWJWKUQWIXOXNWIWMWRLZWNWTLZLX
    OVGXPVMXQDBUREAURUSWMWRWNWTUTVAVBWOXAWJWKUTVCWFWAWCWELZLXDXGLZXRLXIWAWCWEUQ
    WAXSXRVTAURVBXDXGWCWEUTVCVDVEWGVLWHVRBCDVFABEVFUSACVTVFVD $.
    $( [19-Mar-98] $)

  $( The composition of a one-to-one onto function and its converse equals
     the identity relation restricted to the function's range. $)
  f1ococnv2 $p |- ( F : A -1-1-onto-> B -> ( F o. `' F ) = ( I |` B ) ) $=
    ( wf1o ccnv ccom cid cdm cres wss wceq wf wfun f1of ffun wrel df-fun
    pm3.27bd 3syl iss sylib crn fdm syl wfo f1ocnv wf1 df-f1o forn eqtr4d
    dmcoeq eqtrd reseq2 ) ABCDZCCEZFZGUPHZIZGBIZUNUPGJZUPURKUNABCLZCMZUTABCNZAB
    COVBCPUTCQRSUPTUAUNUQBKURUSKUNUQUOHZBUNCHZUOUBZKUQVDKUNVEAVFUNVAVEAKVCABCUC
    UDUNBAUODZBAUOUEZVFAKABCUFZVGBAUOUGVHBAUOUHRBAUOUISUJCUOUKUDUNVGBAUOLVDBKVI
    BAUONBAUOUCSULUQBGUMUDUL $.
    $( [20-Dec-03] $) $( [13-Dec-03] $)

  $( The composition of a one-to-one onto function's converse and itself
     equals the identity relation restricted to the function's domain. $)
  f1ococnv1 $p |- ( F : A -1-1-onto-> B -> ( `' F o. F ) = ( I |` A ) ) $=
    ( wf1o ccnv ccom cid cres wrel wceq f1orel dfrel2OLD sylib coeq2d f1ocnv
    f1ococnv2 syl eqtrd ) ABCDZCEZCFTTEZFZGAHZSCUATSCICUAJABCKCLMNSBATDUBUCJABC
    OBATPQR $.
    $( [17-May-04] $) $( [13-Dec-03] $)

  $( If the codomain of a one-to-one function exists, so does its domain.
     This theorem is equivalent to the Axiom of Replacement ~ ax-rep . $)
  f1dmex $p |- ( B e. C -> ( F : A -1-1-> B -> A e. V ) ) $=
    ( wcel wf1 crn cvv wss ssexg wf f1f frn syl syl5 ccnv wfo fornex wf1o
    f1f1orn f1ocnv f1ofo 3syl com12 sylcom ) BCEZABDFZDGZHEZAHEZUFUHBIZUIUGUHBC
    JUGABDKUKABDLABDMNOUIUGUJUIUHADPZQZUJUGUHAHULRUGAUHDSUHAULSUMABDTAUHDUAUHAU
    LUBUCOUDUE $.
    $( [5-Sep-04] $) $( [4-Sep-04] $)

  ${
    $d x F $.  $d x A $.  $d x B $.
    f11o.1 $e |- F e. V $.
    $( Relationship between a mapping and an onto mapping.  Figure 38 of
       [Enderton] p. 145. $)
    ffoss $p |- ( F : A --> B <-> E. x ( F : A -onto-> x /\ x (_ B ) ) $=
      ( wf cv wfo wss wa wex crn wfn df-f fnforn biran bitr cvv wcel rnexg
      ax-mp wceq foeq3 sseq1 bi2and cla4ev sylbi fss fof sylan 19.23aiv impbi
      ) BCDFZBAGZDHZUNCIZJZAKZUMBDLZDHZUSCIZJZURUMDBMZVAJVBBCDNVCUTVABDOPQUQVBA
      USDRSUSRSEDRTUAUNUSUBUOUTUPVAUNUSBDUCUNUSCUDUEUFUGUQUMABUNDFUPUMUOBUNCDUH
      BUNDUIUJUKUL $.
      $( [10-May-98] $)

    $( Relationship between one-to-one and one-to-one onto function. $)
    f11o $p |- ( F : A -1-1-> B <-> E. x ( F : A -1-1-onto-> x /\ x (_ B ) ) $=
      ( wf ccnv wfun wa cv wfo wss wex wf1 wf1o ffoss biran df-f1 f1o3 an23
      bitr biex 19.41v 3bitr4 ) BCDFZDGHZIBAJZDKZUGCLZIZAMZUFIZBCDNBUGDOZUIIZAM
      ZUEUKUFABCDEPQBCDRUOUJUFIZAMULUNUPAUNUHUFIZUIIUPUMUQUIBUGDSQUHUFUITUAUBUJ
      UFAUCUAUD $.
      $( [4-Apr-98] $)
  $}

  $( The empty set maps one-to-one into any class. $)
  f10 $p |- (/) : (/) -1-1-> A $=
    ( c0 wf1 wf ccnv wfun wa f0 fun0 wceq wb cnv0 funeq ax-mp mpbir pm3.2i
    df-f1 ) BABCBABDZBEZFZGRTAHTBFZISBJTUAKLSBMNOPBABQO $.
    $( [7-Apr-98] $)

  $( One-to-one onto mapping of the empty set. $)
  f1o00 $p |- ( F : (/) -1-1-onto-> A <-> ( F = (/) /\ A = (/) ) ) $=
    ( c0 wf1o wfn ccnv wa wceq f1o4 fn0 biimp adantr cdm wb cnveq cnv0 syl6eq
    sylbi fneq1 syl biimpa fndm dm0 syl5reqr jca biimpr cleqid mpbir fneq2
    sylan9bb mpbiri impbi bitr ) CABDBCEZBFZAEZGZBCHZACHZGZCABIUQUTUQURUSUNURUP
    UNURBJZKLUQCMZACUQCAEZVBAHUNUPVCUNUOCHZUPVCNZUNURVDVAURUOCFCBCOPQZRAUOCSZTU
    AACUBTUCUDUEUTUNUPURUNUSUNURVAUFLUTUPCCEZVHCCHCUGCJUHURUPVCUSVHURVDVEVFVGTA
    CCUIUJUKUEULUM $.
    $( [15-Apr-98] $)

  $( One-to-one onto mapping of the empty set. $)
  f1o0 $p |- (/) : (/) -1-1-onto-> (/) $=
    ( c0 wf1o wceq wa cleqid pm3.2i f1o00 mpbir ) AAABAACZIDIIAEZJFAAGH $.
    $( [16-Feb-04] $) $( [10-Sep-04] $)

  $( A restriction of the identity relation is a one-to-one onto function. $)
  f1oi $p |- ( I |` A ) : A -1-1-onto-> A $=
    ( cid cres wf1o wfo ccnv wfun wa wfn crn wceq fnresi rnresi pm3.2i df-fo
    mpbir funi wb cnvi funeq ax-mp funres11 f1o3 ) AABACZDAAUDEZUDFGZHUEUFUEUDA
    IZUDJAKZHUGUHALAMNAAUDOPBFZGZUFUJBGZQUIBKUJUKRSUIBTUAPABUBUANAAUDUCP $.
    $( [30-Apr-98] $)

  $( The identity relation is a one-to-one onto function on the universe. $)
  f1ovi $p |- I : V -1-1-onto-> V $=
    ( cvv cid cres wf1o f1oi wceq wb wrel reli resid ax-mp f1oeq1 mpbi ) AABACZ
    DZAABDZAENBFZOPGBHQIBJKAANBLKM $.
    $( [17-May-04] $) $( [16-May-04] $)

  ${
    f1osn.1 $e |- A e. V $.
    f1osn.2 $e |- B e. V $.
    $( A singleton of an ordered pair is one-to-one onto function. $)
    f1osn $p |- { <. A , B >. } : { A } -1-1-onto-> { B } $=
      ( csn cop wf1o wfn ccnv wa wfun cdm wceq funsn dmsnop pm3.2i df-fn mpbir
      wb cnvsn fneq1 ax-mp f1o4 ) AEZBEZABFEZGUFUDHZUFIZUEHZJUGUIUGUFKZUFLUDMZJ
      UJUKABCDNABOPUFUDQRUIBAFEZUEHZUMULKZULLUEMZJUNUOBADCNBAOPULUEQRUHULMUIUMS
      ABCDTUEUHULUAUBRPUDUEUFUCR $.
      $( [18-May-98] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    fv2.1 $e |- A e. V $.
    $( Alternate definition of function value.  Definition 10.11 of [Quine]
       p. 68. $)
    fv2 $p |- ( F ` A ) = U. { x | A. y ( A F y <-> y = x ) } $=
      ( cfv csn cima cv wceq cab cuni wbr weq wb wal df-fv wcel dfcleq wa wex
      wrex dfima2 cleqabi df-rex bitr elsn biran biex breq1 ceqsexv 3bitr
      bi2bi bial biabi unieqi eqtr ) CDFDCGZHZAIZGZJZAKZLCBIZDMZBANZOZBPZAKZLAC
      DQVCVIVBVHAVBVDUSRZVDVARZOZBPVHBUSVASVLVGBVJVEVKVFVJUTURRZUTVDDMZTZAUAZUT
      CJZVNTZAUAVEVJVNAURUBZVPVSBUSABDURUCUDVNAURUEUFVOVRAVMVQVNACUGUHUIVNVEACE
      UTCVDDUJUKULBUTUGUMUNUFUOUPUQ $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x A $.  $d x F $.  $( ` x ` is dummy. $)
    $( A function's value at a proper class is the empty set. $)
    fvprc $p |- ( -. A e. V -> ( F ` A ) = (/) ) $=
      ( vx cvv wcel wn cfv c0 cuni csn cima cv wceq cab wex visset snnz snprc
      imaeq2 sylbi ima0 syl6eq cleq1d cleqcom syl6bb mtbiri nexdv noel3 bicon1
      sylib unieqd df-fv syl5eq uni0 ) ADEFZABGZHIZHUOBAJZKZCLZJZMZCNZIUQUPUOVC
      HUOVBCOZFVCHMZUOVBCUOVBVAHMZUTCPQUOVBHVAMVFUOUSHVAUOUSBHKZHUOURHMUSVGMARU
      RHBSTBUAUBUCHVAUDUEUFUGVEVDVBCUHUIUJUKCABULUMUNUB $.
      $( [20-May-98] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    elfv.1 $e |- B e. V $.
    $( Membership in a function value. $)
    elfv $p |- ( A e. ( F ` B ) <->
               E. x ( A e. x /\ A. y ( B F y <-> y = x ) ) ) $=
      ( cfv wcel cv wbr weq wb wal cab cuni wa wex fv2 eleq2i eluniab bitr ) CD
      EGZHCDBIEJBAKLBMZANOZHCAIHUCPAQUBUDCABDEFRSUCACTUA $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.  $d x G $.  $( ` x ` is dummy. $)
    $( Equality theorem for function value. $)
    fveq1 $p |- ( F = G -> ( F ` A ) = ( G ` A ) ) $=
      ( vx wceq csn cima cv cab cuni cfv imaeq1 cleq1d biabdv unieqd df-fv
      3eqtr4g ) BCEZBAFZGZDHFZEZDIZJCSGZUAEZDIZJABKACKRUCUFRUBUEDRTUDUABCSLMNOD
      ABPDACPQ $.
      $( [29-Dec-96] $)

    $( Equality theorem for function value. $)
    fveq2 $p |- ( A = B -> ( F ` A ) = ( F ` B ) ) $=
      ( vx wceq csn cima cv cab cuni cfv sneq imaeq2 syl cleq1d biabdv unieqd
      df-fv 3eqtr4g ) ABEZCAFZGZDHFZEZDIZJCBFZGZUCEZDIZJACKBCKTUEUITUDUHDTUBUGU
      CTUAUFEUBUGEABLUAUFCMNOPQDACRDBCRS $.
      $( [29-Dec-96] $)
  $}

  ${
    fveq1i.1 $e |- F = G $.
    $( Equality inference for function value. $)
    fveq1i $p |- ( F ` A ) = ( G ` A ) $=
      ( wceq cfv fveq1 ax-mp ) BCEABFACFEDABCGH $.
      $( [2-Sep-03] $)
  $}

  ${
    fveq1d.1 $e |- ( ph -> F = G ) $.
    $( Equality deduction for function value. $)
    fveq1d $p |- ( ph -> ( F ` A ) = ( G ` A ) ) $=
      ( wceq cfv fveq1 syl ) ACDFBCGBDGFEBCDHI $.
      $( [2-Sep-03] $)
  $}

  ${
    fveq2i.1 $e |- A = B $.
    $( Equality inference for function value. $)
    fveq2i $p |- ( F ` A ) = ( F ` B ) $=
      ( wceq cfv fveq2 ax-mp ) ABEACFBCFEDABCGH $.
      $( [28-Jul-99] $)
  $}

  ${
    fveq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function value. $)
    fveq2d $p |- ( ph -> ( F ` A ) = ( F ` B ) ) $=
      ( wceq cfv fveq2 syl ) ABCFBDGCDGFEBCDHI $.
      $( [29-May-99] $)
  $}

  ${
    $d y z F $.  $d y z A $.  $d x y z $.  $( ` z ` is dummy. $)
    hbfv.1 $e |- ( y e. F -> A. x y e. F ) $.
    hbfv.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for function value. $)
    hbfv $p |- ( y e. ( F ` A ) -> A. x y e. ( F ` A ) ) $=
      ( vz cv csn cima wceq cab cuni wcel wal cfv hbsn hbima ax-17 hbeq hbab
      hbuni df-fv eleq2i bial 3imtr4 ) BHZDCIZJZGHIZKZGLZMZNZUNAOUGCDPZNZUPAOAB
      ULUKAGBABBUIUJABDUHEABCFQRUGUJNASTUAUBUOUMUGGCDUCUDZUPUNAUQUEUF $.
      $( [14-Nov-95] $)
  $}

  ${
    $d x A $.  $d x F $.  $( ` x ` is dummy. $)
    $( The value of a class exists.  Corollary 6.13 of [TakeutiZaring]
       p. 27. $)
    fvex $p |- ( F ` A ) e. V $=
      ( vx cfv csn cima cv wceq cab cuni cvv df-fv wmo wcel wi wal unieq
      visset unisn syl6req ax-gen moeq immo mp2 moabex ax-mp uniex eqeltr ) ABD
      BAEFZCGZEZHZCIZJKCABLUMULCMZUMKNULUJUIJZHZOZCPUPCMUNUQCULUOUKJUJUIUKQUJCR
      STUACUOUBULUPCUCUDULCUEUFUGUH $.
      $( [30-Dec-96] $)
  $}

  ${
    $d x y z F $.  $d x y z A $.
    fv3.1 $e |- A e. V $.
    $( Alternate definition of the value of a function.  Definition 6.11 of
       [TakeutiZaring] p. 26. $)
    fv3 $p |- ( F ` A ) =  { x | ( E. y ( x e. y /\ A F y ) /\
              E! y A F y ) } $=
      ( vz wel cv wbr wa wex weu cfv wcel weq wb wal elfv wi bi2 19.20i visset
      breq2 ceqsalv sylib imlan 19.22i eleq2 bi2and cbvexv 19.40 pm3.27d df-eu
      sylibr jca hbeu1 ax-17 hba1 hban hbex hbim bi1 ax-14 syl6 com23 imp3a
      a4s anc2ri com12 19.22dv syl5ib 19.23ai imp impbi bitr biabri ) ABGZCBHZD
      IZJZBKZVSBLZJZACDMZAHZWDNAFGZVSBFOZPZBQZJZFKZWCFBWECDERWKWCWKWAWBWKWFCFHZ
      DIZJZFKWAWJWNFWIWMWFWIWGVSSZBQWMWHWOBVSWGTUAVSWMBWLFUBVRWLCDUCUDUEUFUGWNV
      TFBFBOWFVQWMVSWLVRWEUHWLVRCDUCUIUJUEWKWIFKZWBWKWFFKWPWFWIFUKULVSBFUMZUNUO
      WAWBWKVTWBWKSBWBWKBVSBUPWJBFWFWIBWFBUQWHBURUSUTVAVTWPWKWBVTWIWJFWIVTWJWIV
      TWFWHVTWFSBWHVQVSWFWHVSVQWFWHVSWGVQWFSVSWGVBBFAVCVDVEVFVGVHVIVJWQVKVLVMVN
      VOVP $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x F $.  $d x A $.  $d x B $.
    $( The value of a restricted function. $)
    fvres $p |- ( A e. B -> ( ( F |` B ) ` A ) = ( F ` A ) ) $=
      ( vx wcel cres csn cima cv wceq cab cuni cfv crn wss snssi resabs1 rneq
      3syl df-ima 3eqtr4g cleq1d biabdv unieqd df-fv ) ABEZCBFZAGZHZDIGZJZDKZLC
      UHHZUJJZDKZLAUGMACMUFULUOUFUKUNDUFUIUMUJUFUGUHFZNZCUHFZNZUIUMUFUHBOUPURJU
      QUSJABPCUHBQUPURRSUGUHTCUHTUAUBUCUDDAUGUEDACUEUA $.
      $( [16-Feb-04] $) $( [2-Aug-94] $)
  $}

  $( The value of a member of the domain of a subclass of a function. $)
  funssfv $p |- ( ( ( Fun F /\ G (_ F ) /\ A e. dom G ) ->
                ( F ` A ) = ( G ` A ) ) $=
    ( wfun wss wa cdm wcel cfv cres funssres fveq1d cleqcomd fvres sylan9eq )
    BDCBEFZACGZHZFACIZABIZPRSABQJZIZTPUBSPAUACBCKLMAQBNOM $.
    $( [15-Aug-94] $)

  ${
    $d y z F $.  $d y z A $.
    tz6.12.1 $e |- A e. V $.
    $( Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12-1 $p |- ( ( A F y /\ E! y A F y ) -> ( F ` A ) = y ) $=
      ( vz cv wbr weu wa cfv wcel wel wi wex fv3 cleqabi exancom biran ancom
      bitr eupick sylbi com12 adantr 19.8a imran anasss sylibr exp impbid
      cleqrd ) BAFZCGZUMAHZIZEBCJZULUOEFUPKZEALZUMUQURMUNUQUMURUQURUMIZANZUNIZU
      MURMZVAEUPEABCDOPZVAUNUMURIANZIZVBVAVDUNIVEUTVDUNURUMAQRVDUNSTUMURAUAUBUB
      UCUDURUOUQURUOUQURUOIVAUQURUMUNVAUSUTUNUSAUEUFUGVCUHUIUCUJUK $.
      $( [1-May-04] $) $( [30-Apr-04] $)

    $( Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12 $p |- ( ( <. A , y >. e. F /\ E! y <. A , y >. e. F ) ->
                 ( F ` A ) = y ) $=
      ( cv cop wcel weu wa wbr cfv wceq df-br bieu bi2an tz6.12-1 sylbir ) BAEZ
      FCGZSAHZIBRCJZUAAHZIBCKRLUASUBTBRCMZUASAUCNOABCDPQ $.
      $( [10-Jul-94] $)
  $}

  ${
    $d x y z w $.  $d z w v F $.  $( ` w v ` are dummy. $)
    tz6.12f.1 $e |- ( w e. F -> A. y w e. F ) $.
    $( Function value requiring only that ` y ` not be 'free' in ` F `
       (but not necessarily absent from it). $)
    tz6.12f $p |- ( ( <. x , y >. e. F /\ E! y <. x , y >. e. F ) ->
                 ( F ` x ) = y ) $=
      ( vz cv cop wcel weu wa cfv wceq wi ax-17 weq opeq2 eleq1d wb hbel cbveu
      a1i bi2and cleq2 bi2imd visset tz6.12 chv2 ) AGZFGZHZDIZULFJZKZUIDLZUJMZN
      UIBGZHZDIZUSBJZKZUOUQMZNZFBVCFOFBPZUNVAUPVBVDULUSUMUTVDUKURDUJUQUIQRZUMUT
      SVDULUSFBBCCUKDCGUKIBOETUSFOVEUAUBUCUJUQUOUDUEFUIDAUFUGUH $.
      $( [30-Aug-99] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.
    $( Theorem 6.12(2) of [TakeutiZaring] p. 27. $)
    tz6.12-2 $p |- ( -. E! y A F y -> ( F ` A ) = (/) ) $=
      ( vx vz cvv wcel cv wbr weu wn cfv c0 wceq wi wss cab wal ax-17 eq0
      visset weq pm4.2i elab bineg bial bitr2 sylib sseq2d fveq2 breq1 bieudv
      biabdv sseq12d wel wa wex fv3 pm3.27 ss2abi eqsstr vtoclg syl5bi com12
      ss0 syl6 fvprc a1d pm2.61i ) BFGZBAHZCIZAJZKZBCLZMNZOVJVNVOMPZVPVNVJVQVNV
      OVMDQZPZVQVJVNVRMVOVNVNERZVRMNZVNESWAEHZVRGZKZERVTEVRTWDVNEWCVMVMVMDWBEUA
      ZDEUBVMUCUDUEUFUGUHUIWBCLZWBVKCIZAJZDQZPVSEBFWBBNZWFVOWIVRWBBCUJWJWHVMDWJ
      WGVLAWBBVKCUKULUMUNWFDAUOWGUPAUQZWHUPZDQWIDAWBCWEURWLWHDWKWHUSUTVAVBVCVDV
      OVEVFVJKVPVNBCVGVHVI $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d y F $.  $d y A $.
    tz6.12c.1 $e |- A e. V $.
    $( Corollary of Theorem 6.12(1) of [TakeutiZaring] p. 27. $)
    tz6.12c $p |- ( E! y A F y -> ( ( F ` A ) = y <-> A F y ) ) $=
      ( cv wbr weu cfv wceq wi wex euex hbeu1 ax-17 hbim tz6.12-1 exp com12
      breq2 biimprd syl6 pm2.43d 19.23ai mpcom biimpcd syl impbid ) BAEZCFZAGZB
      CHZUHIZUIUJBUKCFZULUIJUIAKUJUMUIALUIUJUMJAUJUMAUIAMUMANOUJUIUMUJUIUMUJUIU
      LUIUMJUIUJULUIUJULABCDPQRZULUMUIUKUHBCSZTUAUBRUCUDULUMUIUOUEUFUNUG $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.  $( ` y ` is dummy. $)
    tz6.12i.1 $e |- A e. V $. $( (Note: hypothesis could be eliminated) $)
    $( Corollary of Theorem 6.12(2) of [TakeutiZaring] p. 27. $)
    tz6.12i $p |- ( -. B = (/) -> ( ( F ` A ) = B -> A F B ) ) $=
      ( vy cfv wceq c0 wn wbr cvv wcel wi fvex eleq1 mpbii cv cleq2 cleq1
      binegd breq2 bi2imd weu wb tz6.12c tz6.12-2 nsyl4 biimpd syl6bir pm2.43a
      vtoclg mpcom com12 ) ACFZBGZBHGZIZABCJZBKLZUOUQURMZUOUNKLUSACNUNBKOPUNEQZ
      GZVAHGZIZAVACJZMZMUOUTMEBKVABGZVBUOVFUTVABUNRVGVDUQVEURVGVCUPVABHSTVABACU
      AUBUBVDVBVEVBVDUNHGZIZVBVEMVBVHVCUNVAHSTVIVBVEVEEUCVBVEUDVHEACDUEEACUFUGU
      HUIUJUKULUM $.
      $( [1-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y F $.  $( ` x y ` are dummy. $)
    $( The value of a class outside its domain is the empty set. $)
    ndmfv $p |- ( -. A e. dom F -> ( F ` A ) = (/) ) $=
      ( vy vx cvv wcel cdm wn cfv c0 wceq wi cv wbr weu wex eleq1 breq1 biexdv
      visset eldm vtoclbg euex syl5bir con3d tz6.12-2 syl6 fvprc a1d pm2.61i )
      AEFZABGZFZHZABIJKZLUKUNACMZBNZCOZHUOUKURUMUKUMUQCPZURDMZULFUTUPBNZCPUMUSD
      AEUTAULQUTAKVAUQCUTAUPBRSCUTBDTUAUBUQCUCUDUECABUFUGUKHUOUNABUHUIUJ $.
      $( [24-Aug-95] $)
  $}

  ${
    ndmfvrcl.1 $e |- dom F = S $.
    ndmfvrcl.2 $e |- -. (/) e. S $.
    $( Reverse closure law for function with the empty set not in its
       domain. $)
    ndmfvrcl $p |- ( ( F ` A ) e. S -> A e. S ) $=
      ( cfv wcel cdm wn c0 ndmfv eleq1d mtbiri a3i eleq2i sylib ) ACFZBGZACHZGZ
      ABGTRTIZRJBGEUAQJBACKLMNSBADOP $.
      $( [26-Apr-96] $)
  $}

  $( A non-element of a restriction has empty value. $)
  nfvres $p |- ( -. A e. B -> ( ( F |` B ) ` A ) = (/) ) $=
    ( wcel wn cres cdm cfv c0 wceq cin wa dmres eleq2i elin bitr pm3.26bd
    con3i ndmfv syl ) ABDZEACBFZGZDZEAUBHIJUDUAUDUAACGZDZUDABUEKZDUAUFLUCUGACBM
    NABUEOPQRAUBST $.
    $( [13-Nov-95] $)

  $( Equal values imply equal values in a restriction. $)
  fveqres $p |- ( ( F ` A ) = ( G ` A ) ->
                ( ( F |` B ) ` A ) = ( ( G |` B ) ` A ) ) $=
    ( wcel cfv wceq cres wi fvres cleq12d biimprd wn c0 nfvres eqtr4d a1d
    pm2.61i ) ABEZACFZADFZGZACBHFZADBHFZGZISUEUBSUCTUDUAABCJABDJKLSMZUEUBUFUCNU
    DABCOABDOPQR $.
    $( [13-Nov-95] $)

  ${
    $d x y A $.  $d x y F $.  $d x y B $.
    funbrfv.1 $e |- B e. V $.
    $( The second argument of a binary relation on a function is the function's
       value. $)
    funbrfv $p |- ( Fun F -> ( A F B -> ( F ` A ) = B ) ) $=
      ( vx vy wfun wbr cfv wceq cvv wcel wa wrel brrelex funrel sylan wi cv
      breq1 biland fveq2 cleq1d bi2imd breq2 cleq2 weu visset tz6.12-1 funeu
      sylan5 anabss7 vtocl2g mpan2 mpcom exp ) CGZABCHZACIZBJZAKLZUQURMZUTCNURV
      AUQABCOCPQVABKLVBUTRZDUQESZFSZCHZMZVDCIZVEJZRUQAVECHZMZUSVEJZRVCEFABKKVDA
      JZVGVKVIVLVMVFVJUQVDAVECTUAVMVHUSVEVDACUBUCUDVEBJZVKVBVLUTVNVJURUQVEBACUE
      UAVEBUSUFUDUQVFVIVFVFFUGVIVGFVDCEUHUIEFCUJUKULUMUNUOUP $.
      $( [18-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y F $.  $d x y B $.
      $( ` x y ` are dummy. $)
    funopfv.1 $e |- B e. V $.
    $( The second element in an ordered pair member of a function is the
       function's value. $)
    funopfv $p |- ( Fun F -> ( <. A , B >. e. F -> ( F ` A ) = B ) ) $=
      ( wfun wbr cfv wceq cop wcel funbrfv df-br syl5ibr ) CEABCFACGBHABICJABCD
      KABCLM $.
      $( [19-Jul-96] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.  $( ` x ` is dummy. $)
    $( The second element in an ordered pair member of a function is the
       function's value. $)
    funopfvg $p |- ( ( B e. C /\ Fun F ) ->
                   ( <. A , B >. e. F -> ( F ` A ) = B ) ) $=
      ( vx wcel wfun cop cfv wceq wi cv opeq2 eleq1d cleq2 bi2imd bilimd
      visset funopfv vtoclg imp ) BCFDGZABHZDFZADIZBJZKZUBAELZHZDFZUEUHJZKZKUBU
      GKEBCUHBJZULUGUBUMUJUDUKUFUMUIUCDUHBAMNUHBUEOPQAUHDERSTUA $.
      $( [14-Sep-99] $)
  $}


  ${
    $d x y F $.  $d x A $. $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    fnfvbr.1 $e |- C e. V $.
    $( Equivalence of function value and binary relation. $)
    fnfvbr $p |- ( ( F Fn A /\ B e. A ) ->
                   ( ( F ` B ) = C <-> B F C ) ) $=
      ( vx vy wfn wcel wa cfv cv wceq wbr wb wi cleq2 breq2 bi2bid bilimd weu
      fneu breq1 bieudv fveq2 cleq1d bi2imd visset tz6.12c vtoclg adantl mpd
      vtocl ) DAHZBAIZJZBDKZFLZMZBURDNZOZPUPUQCMZBCDNZOZPFCEURCMZVAVDUPVEUSVBUT
      VCURCUQQURCBDRSTUPUTFUAZVAFABDUBUOVFVAPZUNGLZURDNZFUAZVHDKZURMZVIOZPVGGBA
      VHBMZVJVFVMVAVNVIUTFVHBURDUCZUDVNVLUSVIUTVNVKUQURVHBDUEUFVOSUGFVHDGUHUIUJ
      UKULUM $.
      $( [24-Apr-04] $) $( [19-Apr-04] $)

    $( Equivalence of function value and ordered pair membership. $)
    fnfvop $p |- ( ( F Fn A /\ B e. A ) ->
                   ( ( F ` B ) = C <-> <. B , C >. e. F ) ) $=
      ( wfn wcel wa cfv wceq wbr cop fnfvbr df-br syl6bb ) DAFBAGHBDICJBCDKBCLD
      GABCDEMBCDNO $.
      $( [7-Nov-95] $)
  $}

  ${
    funfvop.1 $e |- B e. V $.
    $( Equivalence of function value and ordered pair membership.  Theorem
       4.3(ii) of [Monk1] p. 42. $)
    funfvop $p |- ( ( Fun F /\ A e. dom F ) ->
                   ( ( F ` A ) = B <-> <. A , B >. e. F ) ) $=
      ( cdm wfn wcel cfv wceq cop wb wfun fnfvop df-fn cleqid mpbiranr sylanbr
      ) CCEZFZARGACHBIABJCGKCLZRABCDMSTRRICRNROPQ $.
      $( [26-Jan-97] $)
  $}

  ${
    $d x y z w A $.  $d x y z w F $.
    $( Representation of a function in terms of its values. $)
    fnopabfv $p |- ( F Fn A <->
                   F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) } ) $=
      ( vz vw wfn cv wcel cfv wceq wa copab cop wb wal wi visset fnop exp
      pm4.71r sylib fnfvop cleqcom syl5bb pm5.32d bitr4d weq eleq1 fveq2
      cleq2d bi2and cleq1 biland opelopab syl6bbr 19.21aivv wrel fnrel relopab
      jctir cleqrel syl mpbird fvex cleqid fnopab2 fneq1 mpbiri impbi ) DCGZDAH
      ZCIZBHZVLDJZKZLZABMZKZVKVSEHZFHZNZDIZWBVRIZOZFPEPZVKWEEFVKWCVTCIZWAVTDJZK
      ZLZWDVKWCWGWCLZWJVKWCWGQWCWKOVKWCWGCVTWADERZFRZSTWCWGUAUBVKWGWIWCVKWGWIWC
      OVKWGLWHWAKWCWICVTWADWMUCWAWHUDUETUFUGVQWGVNWHKZLWJABVTWAWLWMAEUHZVMWGVPW
      NVLVTCUIWOVOWHVNVLVTDUJUKULBFUHWNWIWGVNWAWHUMUNUOUPUQVKDURZVRURZLVSWFOVKW
      PWQCDUSVQABUTVAEFDVRVBVCVDVSVKVRCGABCVOVRVLDVEVRVFVGCDVRVHVIVJ $.
      $( [30-Jan-04] $) $( [29-Jan-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $( ` y ` is dummy. $)
    $( Function value in an image.  Part of Theorem 4.4(iii) of [Monk1]
       p. 42. $)
    fvelima $p |- ( ( Fun F /\ A e. ( F " B ) ) ->
               E. x  e. B ( F ` x ) = A ) $=
      ( vy wfun cima wcel cv cfv wceq wrex wi eleq1 cleq2 birexdv bi2imd
      bilimd wbr cop visset funopfv df-br syl5ib r19.22sdv elima vtoclg
      pm2.43b imp ) DFZBDCGZHZAIZDJZBKZACLZUJULUPUJEIZUKHZUNUQKZACLZMZMUJULUPMZ
      MEBUKUQBKZVAVBUJVCURULUTUPUQBUKNVCUSUOACUQBUNOPQRUJUMUQDSZACLUTURUJVDUSAC
      UJUMUQTDHUSVDUMUQDEUAZUBUMUQDUCUDUEAUQDCVEUFUDUGUHUI $.
      $( [1-May-04] $) $( [29-Apr-04] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y F $.  $( ` y ` is dummy. $)
    $( Singleton of function value. $)
    fnsnfv $p |- ( ( F Fn A /\ B e. A ) -> { ( F ` B ) } = ( F " { B } ) ) $=
      ( vy wfn wcel wa cv cfv wceq cab cop csn cima visset fnfvop cleqcom
      syl5bb biabdv df-sn a1i wrel fnrel imasn syl adantr 3eqtr4d ) CAEZBAFZGZD
      HZBCIZJZDKZBUKLCFZDKZULMZCBMNZUJUMUODUJULUKJUOUMABUKCDOPUKULQRSUQUNJUJDUL
      TUAUHURUPJZUIUHCUBUSACUCDBCUDUEUFUG $.
      $( [22-May-98] $)
  $}

  $( A simplified expression for the value of a function when we know it's
     a function. $)
  funfv $p |- ( Fun F -> ( F ` A ) = U. ( F " { A } ) ) $=
    ( wfun cdm wcel cfv csn cima cuni wceq wa wfn fnsnfv df-fn cleqid mpbiranr
    sylanbr unieqd fvex unisn syl5eqr exp wn wi c0 ndmfv ndmima uni0 syl6eq
    eqtr4d a1i pm2.61d ) BCZABDZEZABFZBAGHZIZJZUMUOUSUMUOKZUPGZIURUPUTVAUQBUNLZ
    UOVAUQJUMUNABMVBUMUNUNJBUNNUNOPQRUPABSTUAUBUOUCZUSUDUMVCUPUEURABUFVCURUEIUE
    VCUQUEABUGRUHUIUJUKUL $.
    $( [22-May-98] $)

  ${
    $d y A $.  $d y F $.
    $( The value of a function.  Definition of [Enderton] p. 43. $)
    funfv2 $p |- ( Fun F -> ( F ` A ) = U. { y | <. A , y >. e. F } ) $=
      ( wfun cfv csn cima cuni cv cop wcel cab funfv wrel wceq funrel imasn
      syl unieqd eqtrd ) CDZBCECBFGZHBAIJCKALZHBCMUAUBUCUACNUBUCOCPABCQRST $.
      $( [22-May-98] $)
  $}

  ${
    $d y z A $.  $d y z F $.  $d y z G $.  $( ` y z ` are dummy. $)
    $( Domains of a function composition. $)
    dmfco $p |- ( ( Fun G /\ A e. dom G ) ->
               ( A e. dom ( F o. G ) <-> ( G ` A ) e. dom F ) ) $=
      ( vz vy wfun cdm wcel wa ccom cv cop wex cfv wb eldmg cvv visset opelcog
      mpan2 biexdv bitrd adantl wceq funfvop cleqcom syl5bb birand fvex opeq1
      eleq1d ceqsexv syl5bbr eldm2 bitr4d ) CFZACGZHZIZABCJZGHZADKZLCHZVBEKZLZB
      HZIZDMZEMZACNZBGHZURVAVIOUPURVAAVDLUTHZEMVIEAUTUQPURVLVHEURVDQHVLVHOERDAV
      DBCUQQSTUAUBUCUSVJVDLZBHZEMVIVKUSVNVHEUSVBVJUDZVFIZDMVHVNUSVPVGDUSVOVCVFU
      SVJVBUDVCVOAVBCDRUEVBVJUFUGUHUAVFVNDVJACUIZVOVEVMBVBVJVDUJUKULUMUAEVJBVQU
      NUGUO $.
      $( [27-Jan-97] $)

    $( Value of a function composition.  Similar to Exercise 5 of
       [TakeutiZaring] p. 28. $)
    fvco $p |- ( ( ( Fun F /\ Fun G ) /\ A e. dom G ) ->
               ( ( F o. G ) ` A ) = ( F ` ( G ` A ) ) ) $=
      ( vz wfun wa cdm wcel ccom cfv wceq cop wi dmfco biland cv wex wb cvv
      fvex opelcog mpan2 adantl visset funfvop cleqcom syl5bb birand biexdv
      opeq1 eleq1d ceqsexv syl5bbr bitr4d cleqid mpbii syl5bir sylbid exp4b
      com3r imp41 funco sylan adantlr mpbird exp wn c0 ndmfv binegd syl6bi imp
      eqtr4d adantll pm2.61d ) BEZCEZFZACGZHZFZABCIZGHZAWBJZACJZBJZKZWAWCWGWAWC
      FWGAWFLWBHZVPVQVTWCWHVQVTVPWCWHMVQVTVPWCWHVQVTFZVPWCFVPWEBGHZFZWHWIWCWJVP
      ABCNZOWIWHWEWFLZBHZWKWIWHADPZLCHZWOWFLZBHZFZDQZWNVTWHWTRZVQVTWFSHXAWEBTZD
      AWFBCVSSUAUBUCWIWOWEKZWRFZDQWTWNWIXDWSDWIXCWPWRWIWEWOKWPXCAWOCDUDUEWOWEUF
      UGUHUIWRWNDWEACTXCWQWMBWOWEWFUJUKULUMUNWKWFWFKWNWFUOWEWFBXBUEUPUQURUSUTVA
      VRWCWGWHRZVTWBEWCXEVRAWFWBXBUEBCVBVCVDVEVFVQVTWCVGZWGMVPWIXFWGWIXFFWDVHWF
      XFWDVHKWIAWBVIUCWIXFWFVHKZWIXFWJVGXGWIWCWJWLVJWEBVIVKVLVMVFVNVO $.
      $( [27-Jan-97] $)
  $}

  $( Value of a function composition.  Part of Theorem 3H of [Enderton]
     p. 47. $)
  fvco2 $p |- ( ( ( Fun F /\ G : A --> B ) /\ C e. A ) ->
             ( ( F o. G ) ` C ) = ( F ` ( G ` C ) ) ) $=
    ( wfun wcel wf ccom cfv wceq wa cdm fvco ffun imlan fdm eleq2d biimparc
    syl2an anandirs an1rs ) DFZCAGZABEHZCDEIJCEJDJKZUCUDUEUFUCEFZLCEMZGZUFUCUEL
    UDUELCDENUEUGUCABEOPUEUIUDUEUHACABEQRSTUAUB $.
    $( [3-Jan-04] $) $( [3-Jan-04] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ch $.
    fvopab3.1 $e |- B e. V $.
    fvopab3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    fvopab3.3 $e |- ( y = B -> ( ps <-> ch ) ) $.
    fvopab3.4 $e |- ( x e. C -> E! y ph ) $.
    fvopab3.5 $e |- F = { <. x , y >. | ( x e. C /\ ph ) } $.
    $( Value of a function given by ordered pair abstraction. $)
    fvopab3 $p |- ( A e. C -> ( ( F ` A ) = B <-> ch ) ) $=
      ( wcel cop cv wa copab cfv wceq cvv wb eleq1 bi2and biland opelopabg
      mpan2 wfn fnopab fnfvop mpan eleq2i syl6bb ibar 3bitr4d ) FHOZFGPZDQZHOZA
      RZDESZOZUQCRZFITGUAZCUQGUBOVCVDUCJVAUQBRVDDEFGHUBUSFUAUTUQABUSFHUDKUEEQGU
      ABCUQLUFUGUHUQVEURIOZVCIHUIUQVEVFUCADEHIMNUJHFGIJUKULIVBURNUMUNUQCUOUP $.
      $( [6-Mar-96] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y ch $.
    fvopab3ig.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    fvopab3ig.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    fvopab3ig.3 $e |- ( x e. C -> E* y ph ) $.
    fvopab3ig.4 $e |- F = { <. x , y >. | ( x e. C /\ ph ) } $.
    $( Value of a function given by ordered pair abstraction. $)
    fvopab3ig $p |- ( ( A e. C /\ B e. D ) -> ( ch -> ( F ` A ) = B ) ) $=
      ( wcel wa cv copab cfv wceq cop wi eleq1 bi2and biland opelopabg biimpar
      exp43 pm2.43a imp wfun wmo funopab moanimv mpbir mpgbir funopfvg mpan2
      adantl syld fveq1i cleq1i syl6ibr ) FHOZGIOZPZCFDQZHOZAPZDERZSZGTZFJSZGTV
      FCFGUAVJOZVLVDVECVNUBZVEVDVOVDVEVDCVNVFVNVDCPZVIVDBPVPDEFGHIVGFTVHVDABVGF
      HUCKUDEQGTBCVDLUEUFUGUHUIUJVEVNVLUBZVDVEVJUKZVQVRVIEULZDVIDEUMVSVHAEULUBM
      VHAEUNUOUPFGIVJUQURUSUTVMVKGFJVJNVAVBVC $.
      $( [23-Oct-99] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    fvopab4g.1 $e |- ( x = A -> B = C ) $.
    fvopab4g.2 $e |- F = { <. x , y >. | ( x e. D /\ y = B ) } $.
    $( Value of a function given by ordered pair abstraction. $)
    fvopab4g $p |- ( ( A e. D /\ C e. R ) -> ( F ` A ) = C ) $=
      ( wcel wa wceq cfv cleqid cv cleq2d cleq1 wmo moeq a1i fvopab3ig mpi ) CF
      KEGKLEEMZCHNEMEOBPZDMZUEEMUDABCEFGHAPZCMDEUEIQUEEERUFBSUGFKBDTUAJUBUC $.
      $( [23-Oct-99] $)

    ${
      fvopab4.3 $e |- C e. V $.
      $( Value of a function given by ordered pair abstraction. $)
      fvopab4 $p |- ( A e. D -> ( F ` A ) = C ) $=
        ( wcel cvv cfv wceq fvopab4g mpan2 ) CFKELKCGMENJABCDEFLGHIOP $.
        $( [23-Oct-99] $)
    $}
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d x y z w C $.
    fvopabg.1 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered pair abstraction. $)
    fvopabg $p |- ( ( A e. D /\ C e. R ) ->
                    ( { <. x , y >. | y = B } ` A ) = C ) $=
      ( cvv wcel cv wceq copab cfv wa visset biantrur biopab fvopab4g elisset
      sylan ) CIJEGJCBKDLZABMZNELCFJABCDEIGUCHUBAKIJZUBOABUDUBAPQRSCFTUA $.
      $( [2-Sep-03] $)

    $( This somewhat non-intuitive theorem tells us the value of its function
       is the empty set when the class ` C ` it would otherwise map to is a
       proper class.  This is a technical lemma that can help eliminate
       redundant sethood antecedents otherwise required by ~ fvopabg . $)
    fvopabn $p |- ( -. C e. V -> ( { <. x , y >. | y = B } ` A ) = (/) ) $=
      ( vz vw cvv wcel wn cv wceq copab cfv c0 wi wa cuni csn cima cab wal
      visset snnz cop wex opeq1 eleq1d ceqsexgv elsn biran biex syl5bb wb
      cleq2d cleq1 opelopabg mpan2 bitrd biabdv eleq1 mpbii 19.23aiv con3i
      noel3 bicon1 sylib sylan9eq dfima3 syl5eq cleq1d cleqcom syl6bb mtbiri
      19.21aiv alnex bitr unieqd df-fv uni0 syl6eq exp fvprc a1d pm2.61i ) CIJZE
      IJZKZCBLZDMZABNZOZPMZQWGWIWNWGWIRZWMPSZPWOWLCTZUAZGLZTZMZGUBZSWPWMWOXBPWO
      XAKZGUCZXBPMZWOXCGWOXAWTPMZWSGUDUEWOXAPWTMXFWOWRPWTWOWSWQJZWSHLZUFZWLJZRZ
      GUGZHUBZPWRWGWIXMXHEMZHUBZPWGXLXNHWGXLCXHUFZWLJZXNWGWSCMZXJRZGUGXQXLXJXQG
      CIXRXIXPWLWSCXHUHUIUJXKXSGXGXRXJGCUKULUMUNWGXHIJZXQXNUOHUDZWKWJEMXNABCXHI
      IALCMDEWJFUPWJXHEUQURUSUTVAWIXNHUGZKXOPMZYBWHXNWHHXNXTWHYAXHEIVBVCVDVEYCY
      BXNHVFVGVHVIGHWLWQVJVKVLPWTVMVNVOVPXDXAGUGZKXEXAGVQXEYDXAGVFVGVRVHVSGCWLV
      TVKWAWBWCWGKWNWICWLWDWEWF $.
      $( [7-Nov-03] $) $( [21-Oct-03] $)
  $}

  ${
    $d z w v A $.  $d y w v u B $.  $d z w v C $.  $d x y w v $.
    $d x u $.  $d x z $.
    fvopabgf.1 $e |- ( z e. A -> A. x z e. A ) $.
    fvopabgf.2 $e |- ( z e. C -> A. x z e. C ) $.
    fvopabgf.3 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered pair abstraction. $)
    fvopabgf $p |- ( ( A e. D /\ C e. R ) ->
                    ( { <. x , y >. | y = B } ` A ) = C ) $=
      ( vv vu vw wcel wa cv wsb cab wceq copab cfv weq wex wel ax-17 visset
      eqvincf hbs1 hbab hbeq sbab cleqcomd sylan9eq 19.23ai sylbi fvopabg
      hbeleq id cleqan12rd cbvopab fveq1i syl5eq ) DGOFHOPDLQZMQEOZANRZMSZTZNLU
      AZUBFDBQZETZABUAZUBNLDVGFGHNQZDTANUCZAQDTZPZAUDVGFTZACVMDCNUEAUFINUGUHVPV
      QAALCVGFVFAMLVEANUIUJZJUKVNVOVGEFVNEVGANMEULZUMKUNUOUPUQDVLVIVKVHABNLVKNU
      FVKLUFALVGVRURVHBUFBLUCZVNVJVDEVGVTUSVSUTVAVBVC $.
      $( [28-Oct-03] $) $( [21-Oct-03] $)

    $( The value of a function given by an ordered pair abstraction is the
       empty set when the class it would otherwise map to is a proper class.
       This version of ~ fvopabn uses bound variable hypotheses instead of
       distinct variable conditions. $)
    fvopabnf $p |- ( -. C e. V -> ( { <. x , y >. | y = B } ` A ) = (/) ) $=
      ( vv vu vw cvv wcel wn cv wsb cab wceq copab cfv c0 weq wa wex wel ax-17
      visset eqvincf hbs1 hbab hbeq sbab cleqcomd sylan9eq 19.23ai sylbi
      fvopabn hbeleq id cleqan12rd cbvopab fveq1i syl5eq ) FMNODJPZKPENZALQZKRZ
      SZLJTZUAUBDBPZESZABTZUALJDVHFLPZDSALUCZAPDSZUDZAUEVHFSZACVNDCLUFAUGGLUHUI
      VQVRAAJCVHFVGAKJVFALUJUKZHULVOVPVHEFVOEVHALKEUMZUNIUOUPUQURDVMVJVLVIABLJV
      LLUGVLJUGAJVHVSUSVIBUGBJUCZVOVKVEEVHWAUTVTVAVBVCVD $.
      $( [7-Nov-03] $) $( [21-Oct-03] $)
  $}

  ${
    $d z A $.  $d y B $.  $d z C $.  $d x y $.
    $d x u $.  $d x z $.
    fvopabf.1 $e |- ( z e. A -> A. x z e. A ) $.
    fvopabf.2 $e |- ( z e. C -> A. x z e. C ) $.
    fvopabf.3 $e |- A e. V $.
    fvopabf.4 $e |- C e. V $.
    fvopabf.5 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered pair abstraction. $)
    fvopabf $p |- ( { <. x , y >. | y = B } ` A ) = C $=
      ( cvv wcel cv wceq copab cfv fvopabgf mp2an ) DLMFLMDBNEOABPQFOIJABCDEFLL
      GHKRS $.
      $( [14-Sep-03] $)
  $}

  ${
    $d x z A $.  $d y B $.  $d x z C $. $d x y z $.
    fvopab.1 $e |- A e. V $.
    fvopab.2 $e |- C e. V $.
    fvopab.3 $e |- ( x = A -> B = C ) $.
    $( The value of a function given by ordered pair abstraction. $)
    fvopab $p |- ( { <. x , y >. | y = B } ` A ) = C $=
      ( vz cv wcel ax-17 fvopabf ) ABICDEIJZCKALNEKALFGHM $.
      $( [17-Apr-95] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( Value of a function given by an ordered pair abstraction. $)
    fvopab2 $p |- ( ( x e. A /\ B e. C ) ->
                   ( { <. x , y >. | ( x e. A /\ y = B ) } ` x ) = B ) $=
      ( vz cv wcel wceq wa copab cfv wex wi elex ax-17 hbopab2 wel hbfv hbeq
      hbim cvv cop weu tz6.12f opabid sylanbr euanv bieu eueq bilan 3bitr4r
      sylan5b exp43 pm2.43a visset eleq1 mpbii syl7 pm2.43d com12 cleq2 sylibd
      19.23ai syl imp ) AGZCHZDEHZVGVHBGZDIZJZABKZLZDIZVIVHVOVIVKBMVHVONZBDEOVK
      VPBVHVOBVHBPBFFVNDBFVGVMVLABFQZFARBPSFGDHBPTUAVKVHVNVJIZVOVHVKVRVHVKVRVHV
      KDUBHZVRVKVKVHVSVRNVHVKVHVSVRVLVGVJUCVMHZBUDZVRVHVSJZVTWAVRVLABFVMVQUEVLA
      BUFZUGVLBUDVHVKBUDZJWAWBVHVKBUHVTVLBWCUIVSWDVHBDUJUKULUMUNUOVKVJUBHVSBUPV
      JDUBUQURUSUTVAVJDVNVBVCVDVEVAVF $.
      $( [2-Feb-04] $) $( [30-Aug-99] $)
  $}

  ${
    fvsn.1 $e |- A e. V $.
    fvsn.2 $e |- B e. V $.
    $( The value of a singleton of an ordered pair is the second member. $)
    fvsn $p |- ( { <. A , B >. } ` A ) = B $=
      ( cop csn wfun wcel cfv wceq funsn opex snid funopfv mp2 ) ABEZFZGPQHAQIB
      JABCDKPABLMABQDNO $.
      $( [12-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $d x y G $.
    $( Equality of functions is determined by their values.  Exercise 4 of
       [TakeutiZaring] p. 28. $)
    cleqfv $p |- ( ( F Fn A /\ G Fn B ) -> ( F = G <->
                 ( A = B /\ A. x e. A ( F ` x ) = ( G ` x ) ) ) ) $=
      ( vy wfn wa wceq cv cfv wral cdm wi cleq12 dmeq syl5bi fndm syl2an wcel
      fveq1 a1d r19.21aiv a1i jcad cop wb wal visset fnfvop adantlr adantll
      bi2bid cleq1 exp a2d com3r wn eleq2d opeldm con3d adantr adantl pm5.21
      syl6 com12 pm2.61i 19.21adv 19.20dv df-ral syl5ib wrel cleqrel fnrel
      sylibrd fneq2 biimparc sylan5 exp32 imp4b impbid ) DBGZECGZHZDEIZBCIZAJZD
      KZWGEKZIZABLZHWDWEWFWKDMZBIZEMZCIZWEWFNWBWCWMWOHWLWNIWFWEWLBWNCODEPQBDRZC
      ERSWEWKNWDWEWJABWEWJWGBTZWGDEUAUBUCUDUEWBWCWFWKWEWBWCWFWKWENZWBEBGZWRWCWF
      HWBWSHZWKWGFJZUFZDTZXBETZUGZFUHZAUHZWEWTWQWJNZAUHXGWKWTXHXFAWTXHXEFWQWTXH
      XENZNWTXHWQXEWTWQWJXEWTWQWJXENWTWQHZWHXAIZWIXAIZUGXEWJXJXKXCXLXDWBWQXKXCU
      GWSBWGXADFUIZUJUKWSWQXLXDUGWBBWGXAEXMUJULUMWHWIXAUNQUOUPUQWTWQURZXIWTXNXC
      URZXDURZHZXIWTXNXOXPWBXNXONWSWBXCWQWBWGWLTWQXCWBWLBWGWPUSWGXADAUIZUTQVAVB
      WSXNXPNWBWSXDWQWSWGWNTWQXDWSWNBWGBERUSWGXAEXRUTQVAVCUEXQXEXHXCXDVDUBVEVFV
      GVHVIWJABVJVKDVLEVLWEXGUGWBWSAFDEVMBDVNBEVNSVOWFWSWCBCEVPVQVRVSVTWA $.
      $( [5-Feb-04] $) $( [3-Aug-94] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y z F $.  $d y z G $.
    cleqfvf.1 $e |- ( y e. F -> A. x y e. F ) $.
    cleqfvf.2 $e |- ( y e. G -> A. x y e. G ) $.
    $( Equality of functions is determined by their values.  Exercise 4 of
       [TakeutiZaring] p. 28.  This version of ~ cleqfv uses bound variable
       hypotheses instead of distinct variable conditions. $)
    cleqfvf $p |- ( ( F Fn A /\ G Fn B ) -> ( F = G <->
                 ( A = B /\ A. x e. A ( F ` x ) = ( G ` x ) ) ) ) $=
      ( vz wfn wa wceq cv cfv wral wb cleqfv wel ax-17 hbfv hbeq weq fveq2
      cleq12d cbvral bilan bilbi sylib ) ECJFDJKEFLZCDLZIMZENZUKFNZLZICOZKZPUIU
      JAMZENZUQFNZLZACOZKZPICDEFQUPVBUIUOVAUJUNUTIACABBULUMABUKEGBIRASZTABUKFHV
      CTUAUTISIAUBULURUMUSUKUQEUCUKUQFUCUDUEUFUGUH $.
      $( [19-May-04] $) $( [29-Jan-04] $)
  $}

  ${
    $d x B $.  $d x F $.  $d x G $.
    $( Equality of restricted functions is determined by their values. $)
    fvreseq $p |- ( ( ( F Fn A /\ G Fn A ) /\ B (_ A ) ->
         ( ( F |` B ) = ( G |` B ) <-> A. x e. B ( F ` x ) = ( G ` x ) ) ) $=
      ( wfn wa wss cres wceq cv cfv wral wb ssfnres im2and com12 imp cleqfv
      wcel fvres cleq12d birala cleqid biantrur bitr3 syl6bbr syl ) DBFZEBFZGZC
      BHZGDCIZCFZECIZCFZGZUMUOJZAKZDLZUSELZJZACMZNUKULUQULUKUQULUIUNUJUPCBDOCBE
      OPQRUQURCCJZUSUMLZUSUOLZJZACMZGZVCACCUMUOSVCVHVIVGVBACUSCTVEUTVFVAUSCDUAU
      SCEUAUBUCVDVHCUDUEUFUGUH $.
      $( [6-Feb-04] $) $( [3-Aug-94] $)
  $}

  ${
    $d x y z A $.  $d x y z F $.  $d x y z B $.  $d x y z C $.
    $( A member of a function's range is a value of the function. $)
    fvelrn $p |- ( F Fn A -> ( C e. ran F <-> E. x e. A ( F ` x ) = C ) ) $=
      ( vy cvv wcel wfn crn cv cfv wceq wrex wb wi eleq1 cleq2 birexdv bi2bid
      bilimd wbr wex wa visset fnbr weu tz6.12-1 exp wfun funeu fnfun sylan
      syl5 anabsi7 jca fnfvbr biimpd imp3a impbid biexdv elrn2 df-rex 3bitr4g
      vtoclg wn elisset fvex mpbii a1i r19.23aiv pm5.21ni a1d pm2.61i ) CFGZDBH
      ZCDIZGZAJZDKZCLZABMZNZOZVOEJZVPGZVSWDLZABMZNZOWCECFWDCLZWHWBVOWIWEVQWGWAW
      DCVPPWIWFVTABWDCVSQRSTVOVRWDDUAZAUBVRBGZWFUCZAUBWEWGVOWJWLAVOWJWLVOWJWLVO
      WJUCZWKWFBVRWDDAUDZEUDZUEVOWJWFWJWJEUFZWFWMWJWPWFEVRDWNUGUHDUIWJWPVOAEDUJ
      BDUKULUMUNUOUHVOWKWFWJVOWKWFWJOVOWKUCWFWJBVRWDDWOUPUQUHURUSUTAWDDWOVAWFAB
      VBVCVDVNVEWBVOVQVNWACVPVFVTVNABVTVNOWKVTVSFGVNVRDVGVSCFPVHVIVJVKVLVM $.
      $( [31-Oct-95] $)
  $}

  ${
    $d x y z A $.  $d y B $.  $d z F $.  $d w x z C $. $d w F $.
    elrnopab.1 $e |- B e. V $.
    elrnopab.2 $e |- F = { <. x , y >. | ( x e. A
             /\ y = B ) } $.
    $( Membership in the range of an operation abstraction. $)
    elrnopab $p |- ( C e. ran F <-> E. x e. A C = B ) $=
      ( vz vw wfn crn wcel wceq wrex wb cv weu eueq1 a1i fnopab cfv fvelrn wa
      copab wal hbopab1 eleq2i bial 3imtr4 wel ax-17 hbfv hbeq weq fveq2
      cleq1d cbvrex cvv fvopab2 mpan2 fveq1i syl5eq cleqcom syl6bb birexa bitr
      ax-mp ) FCKZEFLMZEDNZACOZPBQDNZABCFVMBRAQZCMZBDGSTHUAVIVJIQZFUBZENZICOZVL
      ICEFUCVSVNFUBZENZACOVLVRWAIACAJJVQEAJVPFJQZVOVMUDZABUEZMZWEAUFWBFMZWFAUFW
      CABJUGFWDWBHUHZWFWEAWGUIUJJIUKAULUMWBEMAULUNWAIULIAUOVQVTEVPVNFUPUQURWAVK
      ACVOWADENVKVOVTDEVOVNWDUBZDVTVODUSMWHDNGABCDUSUTVAVNFWDHVBVCUQDEVDVEVFVGV
      EVH $.
      $( [?] $) $( [30-Aug-04] $)
  $}

  ${
    $d x y A $.  $d x y F $.
    $( The range of a choice function (a function that chooses an element from
       each member of its domain) is included in the union of its domain. $)
    chfnrn $p |- ( ( F Fn A /\ A. x e. A ( F ` x ) e. x ) -> ran F (_ U. A ) $=
      ( vy wfn cv cfv wcel wral wa crn cuni wel wrex wceq fvelrn biimpd hbra1
      wi ra4 eleq1 biimpcd syl6 r19.22d sylan9 eluni2 syl6ibr ssrdv ) CBEZAFZCG
      ZUJHZABIZJZDCKZBLZUNDFZUOHZDAMZABNZUQUPHUIURUKUQOZABNZUMUTUIURVBABUQCPQUM
      VAUSABULABRUMUJBHULVAUSSULABTVAULUSUKUQUJUAUBUCUDUEAUQBUFUGUH $.
      $( [31-Aug-99] $)
  $}

  ${
    $d x F $.  $d x A $.  $( ` x ` is dummy. $)
    $( Ordered pair with function value.  Part of Theorem 4.3(i) of [Monk1]
       p. 41. $)
    fvop $p |- ( ( Fun F /\ A e. dom F ) -> <. A , ( F ` A ) >. e. F ) $=
      ( vx wfun cdm wcel wa cv cfv wceq wex cop fvex isseti wi visset funfvop
      opeq2 eleq1d biimprcd syl6bi pm2.43d cleqcom syl5ib 19.23adv mpi ) BDABEF
      GZCHZABIZJZCKAUILZBFZCUIABMNUGUJULCUGUIUHJZULUJUGUMULUGUMAUHLZBFZUMULOAUH
      BCPQUMULUOUMUKUNBUIUHARSTUAUBUHUIUCUDUEUF $.
      $( [14-Oct-96] $)
  $}

  ${
    $d x y F $.  $d x A $.  $( ` x y ` are dummy. $)
    $( A function's value belongs to its range. $)
    fvrn $p |- ( ( Fun F /\ A e. dom F ) -> ( F ` A ) e. ran F ) $=
      ( vx vy wfun cdm wcel cfv crn cv wa wi wceq eleq1 biland fveq2 eleq1d
      bi2imd cop wex fvop visset weq opeq1 cla4ev syl fvex elrn sylibr vtoclg
      anabsi7 ) BEZABFZGZABHZBIZGZULCJZUMGZKZURBHZUPGZLULUNKZUQLCAUMURAMZUTVCVB
      UQVDUSUNULURAUMNOVDVAUOUPURABPQRUTDJZVASZBGZDTZVBUTURVASZBGZVHURBUAVGVJDU
      RCUBDCUCVFVIBVEURVAUDQUEUFDVABURBUGUHUIUJUK $.
      $( [14-Oct-96] $)
  $}

  $( A function's value belongs to its range. $)
  fnfvrn $p |- ( ( F Fn A /\ B e. A ) -> ( F ` B ) e. ran F ) $=
    ( cfv crn wcel fvrn funfni ) BCDCEFABCBCGH $.
    $( [15-Oct-96] $)

  $( A function's value belongs to its codomain. $)
  ffvrn $p |- ( ( F : A --> B /\ C e. A ) -> ( F ` C ) e. B ) $=
    ( wf wcel wa cfv crn wfn fnfvrn ffn sylan wi frn sseld adantr mpd ) ABDEZCA
    FZGCDHZDIZFZUABFZDAJTUCSACDKABDLMSUCUDNTSUBBUAABDOPQR $.
    $( [12-Aug-99] $)

  ${
    $d x y z A $.  $d x y z B $.  $d y z C $.
    fopab2.1 $e |- F = { <. x , y >. | ( x e. A /\ y = C ) } $.
    $( Functionality of an ordered pair abstraction. $)
    fopab2 $p |- ( A. x e. A C e. B <-> F : A --> B ) $=
      ( vz wcel wral wf wfn crn wss wa cv wceq weu cvv elisset eueq sylib
      r19.20si fnopabg copab wi wal wex hbra1 ax-17 ra4 eleq1a syl3 imp3a syl
      19.23ad rnopab cleqabi syl5ib 19.21aiv hbopab2 hbrn dfss2f sylibr rneqi
      syl5ss jca df-f cdm fdm dmopab2 isset biral dmeqi cleq1i 3bitr4r hbopab1
      hbf wb feq1 ax-mp bial 3imtr4 cfv ffvrn adantr fvopab2 fveq1i syl5eq
      eleq1d adantll mpbid exp r19.20da mpd impbi ) EDIZACJZCDFKZWRFCLZFMZDNZOW
      SWRWTXBWRBPZEQZBRZACJWTWQXEACWQESIZXEEDTBEUAUBUCXDABCFGUDUBWRAPZCIZXDOZAB
      UEZMZDXAWRXCXKIZXCDIZUFZBUGXKDNWRXNBWRXIAUHZXMXLWRXIXMAWQACUIXMAUJWRXHWQU
      FZXIXMUFWQACUKXPXHXDXMWQXDXMUFXHEDXCULUMUNUOUPXOBXKXIABUQURUSUTBHXKDBHXJX
      IABHVAVBHPZDIZBUJVCVDFXJGVEVFVGCDFVHVDWSXFACJZWRWSFVIZCQZXSCDFVJXDBUHZACJ
      XJVIZCQXSYAXDABCVKXFYBACBEVLVMXTYCCFXJGVNVOVPUBWSXFWQACCDXJKZYDAUGWSWSAUG
      AHCDXJXIABHVQXQCIAUJXRAUJVRFXJQWSYDVSGCDFXJVTWAZWSYDAYEWBWCWSXHOZXFWQYFXF
      OXGFWDZDIZWQYFYHXFCDXGFWEWFXHXFYHWQVSWSXHXFOZYGEDYIXGXJWDEYGABCESWGXGFXJG
      WHWIWJWKWLWMWNWOWP $.
      $( [2-Feb-04] $) $( [29-Jan-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    $( A function maps to a class to which all values belong. $)
    ffnfv $p |- ( F : A --> B <-> ( F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( vy wf wfn cv cfv wcel wral wa ffn ffvrn exp r19.21aiv jca crn wss
      pm3.26 wceq wrex fvelrn biimpd hbra1 ax-17 wi ra4 eleq1 biimpcd syl6
      r19.23ad sylan9 dfss3 sylibr df-f impbi ) BCDFZDBGZAHZDIZCJZABKZLZURUSVCB
      CDMURVBABURUTBJZVBBCUTDNOPQVDUSDRZCSZLURVDUSVGUSVCTVDEHZCJZEVFKVGVDVIEVFU
      SVHVFJZVAVHUAZABUBZVCVIUSVJVLABVHDUCUDVCVKVIABVBABUEVIAUFVCVEVBVKVIUGVBAB
      UHVKVBVIVAVHCUIUJUKULUMPEVFCUNUOQBCDUPUOUQ $.
      $( [19-May-04] $) $( [3-Dec-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    $( Representation of a mapping in terms of its values. $)
    fopabfv $p |- ( F : A --> B <->
         ( F = { <. x , y >. | ( x e. A /\ y = ( F ` x ) ) }
         /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( wf wfn cv cfv wcel wral wa wceq copab ffnfv fnopabfv biran bitr ) CDEFE
      CGZAHZEIZDJACKZLETCJBHUAMLABNMZUBLACDEOSUCUBABCEPQR $.
      $( [19-May-04] $) $( [21-Feb-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    fsn.1 $e |- A e. V $.
    fsn.2 $e |- B e. V $.
    $( A function maps a singleton to a singleton iff it is the singleton of a
       ordered pair. $)
    fsn $p |- ( F : { A } --> { B } <-> F = { <. A , B >. } ) $=
      ( vx vy csn wf cop wceq cv wcel wb wal wa visset opelf elsn bi2an sylib
      exp opeq12 eleq1d wreu snid feu mpan2 weu eueq1 biantru euanv opeq2
      pm5.32i biran ancom 3bitr4r bieu df-reu bitr4 bitr3 bitr sylibr syl5bir
      com12 impbid opex elsnc opth bitr2 syl6bb 19.21aivv wrel frel relsn
      jctir cleqrel syl mpbird wf1o f1osn f1oeq1 mpbiri f1of impbi ) AHZBHZCIZC
      ABJZHZKZWHWKFLZGLZJZCMZWNWJMZNZGOFOZWHWQFGWHWOWLAKZWMBKZPZWPWHWOXAWHWOXAW
      HWOPWLWFMZWMWGMZPXAWFWGWLWMCGQZRXBWSXCWTFASGBSZTUAUBXAWHWOXAWOWICMZWHXAWN
      WICWLWMABUCUDWHAWMJZCMZGWGUEZXFWHAWFMXIADUFGWFWGACUGUHXFXFWTGUIZPZXIXJXFG
      BEUJUKXKXFWTPZGUIZXIXFWTGULXMXCXHPZGUIXIXLXNGWTXHPWTXFPXNXLWTXHXFWTXGWICW
      MBAUMUDUNXCWTXHXEUOXFWTUPUQURXHGWGUSUTVAVBVCVDVEVFWPWNWIKXAWNWIWLWMVGVHWL
      WMABFQXDEVIVJVKVLWHCVMZWJVMZPWKWRNWHXOXPWFWGCVNABDVOVPFGCWJVQVRVSWKWFWGCV
      TZWHWKXQWFWGWJVTABDEWAWFWGCWJWBWCWFWGCWDVRWE $.
      $( [13-Dec-03] $) $( [10-Dec-03] $)
  $}

  ${
    fsn2.1 $e |- A e. V $.
    $( A function that maps a singleton to a class is the singleton of an
       ordered pair. $)
    fsn2 $p |- ( F : { A } --> B <->
               ( ( F ` A ) e. B /\ F = { <. A , ( F ` A ) >. } ) ) $=
      ( csn wf cfv wcel wa cop wceq snid ffvrn mpan2 wfn ffn crn fnfrn biimp
      wb cima cdm fndm imaeq2 syl imadmrn syl5eqr fnsnfv eqtr4d feq3 mpbid jca
      wss fss ancoms snssi sylan impbi fvex fsn bilan bitr ) AEZBCFZACGZBHZVCVE
      EZCFZIZVFCAVEJEKZIVDVIVDVFVHVDAVCHZVFADLZVCBACMNVDCVCOZVHVCBCPVMVCCQZCFZV
      HVMVOVCCRSVMVNVGKVOVHTVMVNCVCUAZVGVMCCUBZUAZVPVNVMVQVCKVRVPKVCCUCVQVCCUDU
      ECUFUGVMVKVGVPKVLVCACUHNUIVNVGVCCUJUEUKUEULVGBUMZVHVDVFVHVSVDVCVGBCUNUOVE
      BUPUQURVHVJVFAVECDACUSUTVAVB $.
      $( [20-May-04] $) $( [19-May-04] $)
  $}

  $( The value of a constant function. $)
  fvconst $p |- ( ( F : A --> { B } /\ C e. A ) -> ( F ` C ) = B ) $=
    ( csn wf wcel wa cfv wceq ffvrn elsni syl ) ABEZDFCAGHCDIZNGOBJANCDKOBLM $.
    $( [30-May-99] $)

  ${
    $d x A $.
    $( The value of the identity function. $)
    fvi $p |- ( A e. B -> ( I ` A ) = A ) $=
      ( vx cv cid cfv wceq fveq2 id cleq12d cvv wfn wcel wfun cdm wa funi dmi
      pm3.2i df-fn mpbir visset cop wbr weq cleqid ideq df-br mpbi fnfvop
      mpbiri mp2an vtoclg ) CDZEFZUNGZAEFZAGCABUNAGZUOUQUNAUNAEHURIJEKLZUNKMZUP
      USENZEOKGZPVAVBQRSEKTUACUBZUSUTPUPUNUNUCEMZUNUNEUDZVDVECCUEUNUFUNUNVCVCUG
      UAUNUNEUHUIKUNUNEVCUJUKULUM $.
      $( [2-May-04] $) $( [1-May-04] $)
  $}

  $( The value of a restricted identity function. $)
  fvresi $p |- ( B e. A -> ( ( I |` A ) ` B ) = B ) $=
    ( wcel cid cres cfv fvres fvi eqtrd ) BACBDAEFBDFBBADGBAHI $.
    $( [20-May-04] $) $( [19-May-04] $)

  ${
    $d x A $.  $d x B $.  $d x F $.
    fconst2.1 $e |- B e. V $.
    $( A constant function expressed as a cross product.  See also
       ~ fconstfv . $)
    fconst2 $p |- ( F : A --> { B } <-> F = ( A X. { B } ) ) $=
      ( vx csn wf cxp wceq cv cfv wral wa wcel fvconst fconst mpan adantl
      eqtr4d exp r19.21aiv cleqid jctil wfn wb ffn ax-mp cleqfv mpan2 syl
      mpbird feq1 mpbiri impbi ) ABFZCGZCAUOHZIZUPURAAIZEJZCKZUTUQKZIZEALZMZUPV
      DUSUPVCEAUPUTANZVCUPVFMVABVBABUTCOVFVBBIZUPAUOUQGZVFVGABDPZABUTUQOQRSTUAA
      UBUCUPCAUDZURVEUEZAUOCUFVJUQAUDZVKVHVLVIAUOUQUFUGEAACUQUHUIUJUKURUPVHVIAU
      OCUQULUMUN $.
      $( [20-Aug-99] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.
    $( A constant function expressed in terms of its functionality, domain, and
       value.  See also ~ fconst2 . $)
    fconstfv $p |- ( F : A --> { B } <->
                   ( F Fn A /\ A. x e. A ( F ` x ) = B ) ) $=
      ( vy vz csn wf wfn cv cfv wceq wral wa ffn wcel fvconst exp r19.21aiv
      jca c0 wi fneq2 fn0 syl6bb f0 feq1 mpbiri syl6bi feq2 sylibrd adantrd wn
      crn wrex fvelrn weq fveq2 cleq1d rcla4v imp birexdva r19.9rzv bicomd
      sylan9bbr elsn cleqcom bitr2 cleqrd an1rs exp31 imdistand wfo df-fo fof
      sylbir syl6 pm2.61i impbi ) BCGZDHZDBIZAJZDKZCLZABMZNZWAWBWFBVTDOWAWEABWA
      WCBPWEBCWCDQRSTBUALZWGWAUBWHWBWAWFWHWBUAVTDHZWAWHWBDUALZWIWHWBDUAIWJBUADU
      CDUDUEWJWIUAVTUAHVTUFUAVTDUAUGUHUIBUAVTDUJUKULWHUMZWGWBDUNZVTLZNZWAWKWBWF
      WMWKWBWFWMWKWFWBWMWKWFNZWBNZEWLVTWPEJZWLPZCWQLZWQVTPZWBWRFJZDKZWQLZFBUOZW
      OWSFBWQDUPWFXDWSFBUOZWKWSWFXCWSFBWFXABPZNXBCWQWFXFXBCLZWEXGAXABAFUQWDXBCW
      CXADURUSUTVAUSVBWKWSXEWSFBVCVDVEVEWTWQCLWSECVFWQCVGVHUEVIVJVKVLWNBVTDVMWA
      BVTDVNBVTDVOVPVQVRVS $.
      $( [28-Aug-04] $) $( [27-Aug-04] $)
  $}

  $( A function's value in a pre-image belongs to the image. $)
  funfvima $p |- ( ( Fun F /\ B e. dom F ) -> ( B e. A ->
                 ( F ` B ) e. ( F " A ) ) ) $=
    ( wfun cdm wcel wa cfv cima wi cres crn fvres eleq1d df-ima eleq2i
    syl6rbbr fvrn funres sylan syl5bir com12 exp cin dmres elin bitr syl5ibr
    exp3a imp3a pm2.43b ) CDZBCEZFZGBAFZBCHZCAIZFZUOULUNUOURJZULUOUNUSJULUOUNUS
    ULBCAKZEZFZUSUOUNGZULVBUSUOULVBGZURUOURBUTHZUTLZFZVDUOVGUPVFFURUOVEUPVFBACM
    NUQVFUPCAOPQUTDVBVGULBUTRACSTUAUBUCVBBAUMUDZFVCVAVHBCAUEPBAUMUFUGUHUIUBUJUK
    $.
    $( [23-Sep-03] $)

  $( A function's value in an included pre-image belongs to the image. $)
  funfvima2 $p |- ( ( Fun F /\ A (_ dom F ) -> ( B e. A ->
                  ( F ` B ) e. ( F " A ) ) ) $=
    ( wfun cdm wss wcel cfv cima wi funfvima exp com23 a2d ssel syl5 imp ) CDZA
    CEZFZBAGZBCHCAIGZJZRUABSGZJUCTRUAUDUBRUDUAUBRUDUCABCKLMNASBOPQ $.
    $( [3-Feb-97] $)

  ${
    $d x A $.  $d x F $.  $d x G $.
    $( A class including a function contains the function's value in the image
       of the singleton of the argument. $)
    funfvima3 $p |- ( ( Fun F /\ F (_ G ) -> ( A e. dom F ->
                    ( F ` A ) e. ( G " { A } ) ) ) $=
      ( vx wfun wss cdm wcel cfv csn cima wi wa cop ssel fvop syl5 imp wb cv
      wceq sneq imaeq2 syl eleq2d opeq1 eleq1d visset fvex elimasn vtoclbg
      ad2antrr mpbird exp32 com12 ) BEZBCFZABGZHZABIZCAJZKZHZLZUQUPVDUQUPUSVCUQ
      UPUSMZMVCAUTNZCHZUQVEVGUQVFBHVGVEBCVFOABPQRUSVCVGSUQUPUTCDTZJZKZHVHUTNZCH
      VCVGDAURVHAUAZVJVBUTVLVIVAUAVJVBUAVHAUBVIVACUCUDUEVLVKVFCVHAUTUFUGCVHUTDU
      HABUIUJUKULUMUNUOR $.
      $( [24-Mar-04] $) $( [23-Mar-04] $)
  $}

  ${
    $d x y F $.
    $( Upper bound for the class of values of a class. $)
    fvclss $p |- { y | E. x y = ( F ` x ) } (_ ( ran F u. { (/) } ) $=
      ( cv cfv wceq wex cab crn wcel c0 csn wo cun wn wbr visset tz6.12i
      cleqcom syl5ib 19.22dv elrn2 syl6ibr com12 con1d elsn orrd ss2abi df-un
      sseqtr4 ) BDZADZCEZFZAGZBHUKCIZJZUKKLZJZMZBHUPURNUOUTBUOUQUSUOUQOUKKFZUSU
      OVAUQVAOZUOUQVBUOULUKCPZAGUQVBUNVCAVBUMUKFVCUNULUKCAQRUKUMSTUAAUKCBQUBUCU
      DUEBKUFUCUGUHBUPURUIUJ $.
      $( [9-Nov-95] $)
  $}

  ${
    $d x y F $.
    fvclex.1 $e |- F e. V $.
    $( Existence of the class of values of a set. $)
    fvclex $p |- { y | E. x y = ( F ` x ) } e. V $=
      ( cv cfv wceq wex cab crn c0 csn cun cvv wcel rnexg ax-mp p0ex unex
      fvclss ssexi ) BEAECFGAHBICJZKLZMUBUCCNOUBNODCNPQRSABCTUA $.
      $( [9-Nov-95] $)
  $}

  ${
    $d x y z w F $.  $d x y A $.
    fvresex.1 $e |- A e. V $.
    $( Existence of the class of values of a restricted class. $)
    fvresex $p |- { y | E. x y = ( ( F |` A ) ` x ) } e. V $=
      ( vw vz cv cfv wceq copab cres wex cab cvv visset fvex fveq2 fvopab
      fveqres ax-mp cleq2i biex biabi wcel wfun funopabeq resfunexg mp2 fvclex
      eqeltrr ) BHZAHZFHGHZDIZJGFKZCLZIZJZAMZBNULUMDCLIZJZAMZBNOUTVCBUSVBAURVAU
      LUMUPIUMDIZJURVAJGFUMUOVDAPUMDQUNUMDRSUMCUPDTUAUBUCUDABUQCOUEUPUFUQOUEEGF
      UOUGUPCOUHUIUJUK $.
      $( [14-Nov-95] $)
  $}

  ${
    $d x y F $.  $d x y A $.
    abrexexlem1.1 $e |- A e. V $.
    $( Lemma for ~ abrexex .  Shows the existence of a class of existentially
       restricted function values. $)
    abrexexlem1 $p |- { y | E. x e. A y = ( F ` x ) } e. V $=
      ( cv cfv wceq wrex cab cres wex fvresex wcel wa df-rex fvres cleq2d
      biimpar 19.22i sylbi ss2abi ssexi ) BFZAFZDGZHZACIZBJUDUEDCKGZHZALZBJABCD
      EMUHUKBUHUECNZUGOZALUKUGACPUMUJAULUJUGULUIUFUDUECDQRSTUAUBUC $.
      $( [26-Apr-04] $) $( [22-Apr-04] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.
    abrexexlem2.1 $e |- A e. V $.
    abrexexlem2.2 $e |- B e. V $.
    $( Lemma for ~ abrexex .  Almost there, but still requires that ` B ` be
       a set. $)
    abrexexlem2 $p |- { y | E. x e. A y = B } e. V $=
      ( vw vz cv wceq wrex cab copab cfv cvv wcel wa visset biantrur biopab
      fveq1i fvopab2 mp2an eqtr cleq2i birex ax-17 wel hbopab1 hbfv hbeq weq
      fveq2 cleq2d cbvrex bitr3 biabi hbopab2 hbrex cleq1 birexdv cbvab
      abrexexlem1 eqeltr ) BIZDJZACKZBLZGIZHIZVFABMZNZJZHCKZGLZOVHVEVLJZHCKZBLV
      OVGVQBVGVEAIZVKNZJZACKVQVTVFACVSDVEVSVRVROPZVFQZABMZNZDVRVKWCVFWBABWAVFAR
      ZSTUAWADOPWDDJWEFABODOUBUCUDUEUFVTVPAHCVTHUGAGGVEVLGBUHAUGAGVJVKVFABGUIGH
      UHZAUGUJUKAHULVSVLVEVRVJVKUMUNUOUPUQVQVNBGVQGUGVMBHCVJCPBUGBAGVIVLAGUHBUG
      BGVJVKVFABGURWFBUGUJUKUSBGULVPVMHCVEVIVLUTVAVBUDHGCVKEVCVD $.
      $( [14-Oct-03] $) $( [16-Oct-03] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    abrexex.1 $e |- A e. V $.
    $( Existence of an abstraction class of existentially restricted sets.
       ` x ` is normally a free-variable parameter in ` B ` .  This
       simple-looking theorem is actually quite powerful and involves the Axiom
       of Replacement in an intrinsic way, as can be seen by tracing back
       through the path ~ abrexexlem2 , ~ abrexexlem1 , ~ fvresex ,
       ~ resfunexg , and ~ funimaexg .   See also ~ abrexex2 . $)
    abrexex $p |- { y | E. x e. A y = B } e. V $=
      ( vz cv wceq wrex cab cvv wcel crab class2set abrexexlem2 wb visset
      eleq1 mpbii wral ax-1 r19.21aiv rabid2 sylibr cleq2d syl ibi r19.22si
      ss2abi ssexi ) BGZDHZACIZBJUKDKLZFDMZHZACIZBJABCUOEFDNOUMUQBULUPACULUPULU
      NULUPPULUKKLUNBQUKDKRSUNDUOUKUNUNFDTDUOHUNUNFDUNFGDLUAUBUNFDUCUDUEUFUGUHU
      IUJ $.
      $( [18-Oct-03] $) $( [16-Oct-03] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( Existence of an abstraction class of existentially restricted sets.
       ` x ` is normally a free-variable parameter in ` B ` .  The antecedent
       assures us that ` A ` is a set. $)
    abrexexg $p |- ( A e. C -> { y | E. x e. A y = B } e. V ) $=
      ( vz cv wceq wrex cab cvv wcel rexeq biabdv eleq1d visset abrexex vtoclg
      ) BGDHZAFGZIZBJZKLSACIZBJZKLFCETCHZUBUDKUEUAUCBSATCMNOABTDFPQR $.
      $( [8-Nov-03] $) $( [3-Nov-03] $)
  $}

  ${
    $d x y A $.  $d y B $.
    iunex.1 $e |- A e. V $.
    iunex.2 $e |- B e. V $.
    $( The existence of an indexed union.   ` x ` is normally a free-variable
       parameter in ` B ` . $)
    iunex $p |- U. x e. A B e. V $=
      ( vy ciun cv wceq wrex cab cuni cvv dfiun2 abrexex uniex eqeltr ) ABCGFHC
      IABJFKZLMAFBCENRAFBCDOPQ $.
      $( [13-Oct-03] $) $( [13-Oct-03] $)
  $}

  ${
    $d x y z A $.  $d z ph $.
    abrexex2.1 $e |- A e. V $.
    abrexex2.2 $e |- { y | ph } e. V $.
    $( Existence of an existentially restricted abstraction class.  ` ph ` is
       normally has free-variable parameters ` x ` and ` y ` .  This is a
       powerful generalization of ~ abrexex . $)
    abrexex2 $p |- { y | E. x e. A ph } e. V $=
      ( vz wrex cab cv wcel cvv wsb ax-17 hbs1 hbrex weq sbequ12 birexdv cbvab
      df-clab birex biabi eqtr4 ciun df-iun iunex eqeltrr eqeltr ) ABDHZCIZGJAC
      IZKZBDHZGIZLUKACGMZBDHZGIUOUJUQCGUJGNUPCBDBJDKCNACGOPCGQAUPBDACGRSTUNUQGU
      MUPBDAGCUAUBUCUDBDULUEUOLBGDULUFBDULEFUGUHUI $.
      $( [14-Sep-04] $) $( [12-Sep-04] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z F $.  $( ` z ` is dummy. $)
    $( A one-to-one function in terms of function values.  Compare Theorem
       4.8(iv) of [Monk1] p. 43. $)
    f1fv $p |- ( F : A -1-1-> B <-> ( F : A --> B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( vz wf1 wf cv cop wcel wmo wal wa cfv wceq weq wi wral f11 wfn wb ffn
      cdm fndm eleq2d visset opeldm syl5bi im2and ancrd pm3.27 a1i impbid
      fnfvop cleqcom syl5bb bi2anan9 anandis exp pm5.32d bitr4d birimd impexp
      syl6bb bialdv 19.21v wex 19.23v fvex eqvinc birim bitr4 bilim bitr
      bi2aldv opeq1 eleq1d mo4 bial alcom 3bitr r2al 3bitr4g syl pm5.32i ) CDEGC
      DEHZAIZFIZJZEKZALZFMZNWGWHEOZBIZEOZPZABQZRZBCSACSZNAFCDETWGWMWTWGECUAZWMW
      TUBCDEUCXAWKWOWIJZEKZNZWRRZFMZBMZAMZWHCKZWOCKZNZWSRZBMAMWMWTXAXFXLABXAXFX
      KWIWNPZWIWPPZNZWRRZRZFMZXLXAXEXQFXAXEXKXONZWRRXQXAXDXSWRXAXDXKXDNZXSXAXDX
      TXAXDXKXAWKXIXCXJXAWHEUDZKXIWKXAYACWHCEUEZUFWHWIEAUGUHUIXAWOYAKXJXCXAYACW
      OYBUFWOWIEBUGUHUIUJUKXTXDRXAXKXDULUMUNXAXKXOXDXAXKXOXDUBZXAXIXJYCXAXINZXM
      WKXAXJNZXNXCYDWNWIPWKXMCWHWIEFUGZUOWIWNUPUQYEWPWIPXCXNCWOWIEYFUOWIWPUPUQU
      RUSUTVAVBVCXKXOWRVDVEVFXRXKXPFMZRXLXKXPFVGYGWSXKYGXOFVHZWRRWSXOWRFVIWQYHW
      RFWNWPWHEVJVKVLVMVNVOVEVPWMXEBMZAMZFMYIFMZAMXHWLYJFWKXCABWRWJXBEWHWOWIVQV
      RVSVTYIFAWAYKXGAXEFBWAVTWBWSABCCWCWDWEWFVO $.
      $( [29-Oct-96] $)
  $}

  ${
    $d x y w v A $.  $d x y w v B $.  $d z w v F $.  $d x y z $.
    f1fvf.1 $e |- ( z e. F -> A. x z e. F ) $.
    f1fvf.2 $e |- ( z e. F -> A. y z e. F ) $.
    $( A one-to-one function in terms of function values.  Compare Theorem
       4.8(iv) of [Monk1] p. 43. $)
    f1fvf $p |- ( F : A -1-1-> B <-> ( F : A --> B /\
             A. x e. A A. y e. A ( ( F ` x ) = ( F ` y ) -> x = y ) ) ) $=
      ( vw vv wf1 wf cv cfv wceq weq wi wral wa f1fv wel ax-17 hbfv hbeq hbim
      fveq2 cleq2d cleq2 bi2imd cbvral biral wcel hbral cleq1d cleq1 biraldv
      bitr bilan ) DEFKDEFLZIMZFNZJMZFNZOZIJPZQZJDRZIDRZSUSAMZFNZBMZFNZOZABPZQZ
      BDRZADRZSIJDEFTVHVQUSVHVAVLOZIBPZQZBDRZIDRVQVGWAIDVFVTJBDVDVEBBCCVAVCBCUT
      FHCIUAZBUBUCBCVBFHCJUABUBUCUDVEBUBUEVRVSJVRJUBVSJUBUEJBPZVDVRVEVSWCVCVLVA
      VBVKFUFUGVBVKUTUHUIUJUKWAVPIADVTABDVKDULAUBVRVSAACCVAVLACUTFGWBAUBUCACVKF
      GCBUAAUBUCUDVSAUBUEUMVPIUBIAPZVTVOBDWDVRVMVSVNWDVAVJVLUTVIFUFUNUTVIVKUOUI
      UPUJUQURUQ $.
      $( [31-Jul-03] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x y F $.
        $( ` x y ` are dummy. $)
    $( Equality of function values for a one-to-one function. $)
    f1fveq $p |- ( ( F : A -1-1-> B /\ ( C e. A /\ D e. A ) ) ->
                 ( ( F ` C ) = ( F ` D ) <-> C = D ) ) $=
      ( vx vy wf1 wcel wa cfv wceq wi cv weq fveq2 cleq1d cleq1 bi2imd bilimd
      cleq2d cleq2 wral wf f1fv pm3.27bd ra42 syl com12 vtocl2ga imp a1i
      impbid ) ABEHZCAIDAIJZJZCEKZDEKZLZCDLZUNUOUSUTMZUOUNVAUNFNZEKZGNZEKZLZFGO
      ZMZMUNUQVELZCVDLZMZMUNVAMFGCDAAVBCLZVHVKUNVLVFVIVGVJVLVCUQVEVBCEPQVBCVDRS
      TVDDLZVKVAUNVMVIUSVJUTVMVEURUQVDDEPUAVDDCUBSTUNVBAIVDAIJZVHUNVHGAUCFAUCZV
      NVHMUNABEUDVOFGABEUEUFVHFGAAUGUHUIUJUIUKUTUSMUPCDEPULUM $.
      $( [11-Feb-97] $)
  $}

  $( The converse value of the value of a one-to-one onto function. $)
  f1ocnvfv1 $p |- ( ( F : A -1-1-onto-> B /\ C e. A ) ->
                 ( `' F ` ( F ` C ) ) = C ) $=
    ( wf1o wcel wa ccnv ccom cfv cid cres wceq f1ococnv1 fveq1d adantr wfun wf
    fvco2 f1ocnv f1ofun syl f1of jca sylan fvresi adantl 3eqtr3d ) ABDEZCAFZGCD
    HZDIZJZCKALZJZCDJUKJZCUIUMUOMUJUICULUNABDNOPUKQZABDRZGUJUMUPMUIABCUKDSUIUQU
    RUIBAUKEUQABDTBAUKUAUBABDUCUDUEUJUOCMUIACUFUGUH $.
    $( [21-May-04] $) $( [20-May-04] $)

  $( The value of the converse value of a one-to-one onto function. $)
  f1ocnvfv2 $p |- ( ( F : A -1-1-onto-> B /\ C e. B ) ->
                 ( F ` ( `' F ` C ) ) = C ) $=
    ( wf1o wcel wa ccnv ccom cfv cid cres wceq f1ococnv2 fveq1d adantr wfun wf
    fvco2 f1ofun f1ocnv f1of syl jca sylan fvresi adantl 3eqtr3d ) ABDEZCBFZGCD
    DHZIZJZCKBLZJZCUKJDJZCUIUMUOMUJUICULUNABDNOPDQZBAUKRZGUJUMUPMUIBACDUKSUIUQU
    RABDTUIBAUKEURABDUABAUKUBUCUDUEUJUOCMUIBCUFUGUH $.
    $( [21-May-04] $) $( [20-May-04] $)

  $( Relationship between the value of a one-to-one onto function and the
     value of its converse.  (Contributed by Raph Levien, 10-Apr-04.) $)
  f1ocnvfv $p |- ( ( F : A -1-1-onto-> B /\ C e. A ) ->
                  ( ( F ` C ) = D -> ( `' F ` D ) = C ) ) $=
    ( wf1o wcel wa ccnv cfv wceq f1ocnvfv1 cleq2d fveq2 cleqcoms syl5bi ) ABEFC
    AGHZDEIZJZCEJZRJZKZSCKTDKQUACSABCELMUBDTDTRNOP $.
    $( [21-May-04] $) $( [20-May-04] $)

  $( Relationship between the value of a one-to-one onto function and the
     value of its converse.  (Contributed by Raph Levien, 10-Apr-04.) $)
  f1ocnvfvb $p |- ( ( F : A -1-1-onto-> B /\ ( C e. A /\ D e. B ) ) ->
                  ( ( F ` C ) = D <-> ( `' F ` D ) = C ) ) $=
    ( wf1o wcel wa cfv wceq ccnv wi f1ocnvfv adantrr f1ocnvfv2 cleq2d fveq2
    cleqcoms syl5bi adantrl impbid ) ABEFZCAGZDBGZHHCEIZDJZDEKIZCJZUBUCUFUHLUDA
    BCDEMNUBUDUHUFLUCUBUDHZUEUGEIZJZUFUHUIUJDUEABDEOPUKCUGCUGEQRSTUA $.
    $( [21-May-04] $) $( [20-May-04] $)

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $d y ph $.  $d x ps $.
    cbvfo.1 $e |- ( ( F ` x ) = y -> ( ph <-> ps ) ) $.
    $( Change bound variable between domain and range of function. $)
    cbvfo $p |- ( F : A -onto-> B -> ( A. x e. A ph <-> A. y e. B ps ) ) $=
      ( wfo cdm wral crn wf wfun wb fof ffun cv wcel wi wal cfv wceq wa wex
      wbr visset breldm a1i funbrfv jcad 19.22dv elrn2 syl5ib hba1 ax-17
      biimpcd syl3 imp3a a4s 19.23ad syl9 19.21adv brelrn eldm biimprcd impbid
      df-ral 3bitr4g 3syl fdm raleq forn syl 3bitr3d ) EFGIZACGJZKZBDGLZKZACEKZ
      BDFKZVPEFGMZGNZVRVTOEFGPZEFGQWDCRZVQSZATZCUAZDRZVSSZBTZDUAZVRVTWDWIWMWDWI
      WLDWDWKWGWFGUBWJUCZUDZCUEZWIBWDWFWJGUFZCUEWPWKWDWQWOCWDWQWGWNWQWGTWDWFWJG
      CUGZUHUIWFWJGDUGZUJZUKULCWJGWSUMUNWIWOBCWHCUOBCUPWHWOBTCWHWGWNBAWNBTWGWNA
      BHUQURUSUTVAVBVCWDWMWHCWDWGWKWNUDZDUEZWMAWDWQDUEXBWGWDWQXADWDWQWKWNWQWKTW
      DWFWJGWRWSVDUIWTUKULDWFGWRVEUNWMXAADWLDUOADUPWLXAATDWLWKWNABWNATWKWNABHVF
      URUSUTVAVBVCVGACVQVHBDVSVHVIVJVPWCVQEUCVRWAOWEEFGVKACVQEVLVJVPVSFUCVTWBOE
      FGVMBDVSFVLVNVO $.
      $( [23-Feb-97] $)

    $( Change bound variable between domain and range of function. $)
    cbvexfo $p |- ( F : A -onto-> B -> ( E. x e. A ph <-> E. y e. B ps ) ) $=
      ( wfo wn wral wrex cv cfv wceq binegd cbvfo dfrex2 3bitr4g ) EFGIZAJZCEKZ
      JBJZDFKZJACELBDFLTUBUDUAUCCDEFGCMGNDMOABHPQPACERBDFRS $.
      $( [23-Feb-97] $)
  $}

  ${
    $v T $.
    isoeq.vT $f class T $.
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y H $.  $d x y G $.
    $d x y R $.  $d x y S $.  $d x y T $.
    $( Equality theorem for isomorphisms. $)
    isoeq1 $p |- ( H = G ->
          ( H Isom R , S ( A , B ) <-> G Isom R , S ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq1 fveq1 breq12d bilbid
      biraldv bi2and df-iso 3bitr4g ) FEIZABFJZGKZHKZCLZUHFMZUIFMZDLZNZHAOZGAOZ
      PABEJZUJUHEMZUIEMZDLZNZHAOZGAOZPABCDFQABCDEQUFUGUQUPVCABFERUFUOVBGAUFUNVA
      HAUFUMUTUJUFUKURULUSDUHFESUIFESTUAUBUBUCGHABCDFUDGHABCDEUDUE $.
      $( [23-May-04] $) $( [17-May-04] $)

    $( Equality theorem for isomorphisms. $)
    isoeq2 $p |- ( R = T ->
          ( H Isom R , S ( A , B ) <-> H Isom T , S ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso breq birbid biraldv biland
      df-iso 3bitr4g ) CFIZABEJZGKZHKZCLZUFEMUGEMDLZNZHAOZGAOZPUEUFUGFLZUINZHAO
      ZGAOZPABCDEQABFDEQUDULUPUEUDUKUOGAUDUJUNHAUDUHUMUIUFUGCFRSTTUAGHABCDEUBGH
      ABFDEUBUC $.
      $( [23-May-04] $) $( [17-May-04] $)

    $( Equality theorem for isomorphisms. $)
    isoeq3 $p |- ( S = T ->
          ( H Isom R , S ( A , B ) <-> H Isom R , T ( A , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso breq bilbid biraldv biland
      df-iso 3bitr4g ) DFIZABEJZGKZHKZCLZUFEMZUGEMZDLZNZHAOZGAOZPUEUHUIUJFLZNZH
      AOZGAOZPABCDEQABCFEQUDUNURUEUDUMUQGAUDULUPHAUDUKUOUHUIUJDFRSTTUAGHABCDEUB
      GHABCFEUBUC $.
      $( [29-May-04] $) $( [17-May-04] $)

    $( Equality theorem for isomorphisms. $)
    isoeq4 $p |- ( A = C ->
          ( H Isom R , S ( A , B ) <-> H Isom R , S ( C , B ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq2 raleq raleqd bi2and
      df-iso 3bitr4g ) ACIZABFJZGKZHKZDLUFFMUGFMELNZHAOZGAOZPCBFJZUHHCOZGCOZPAB
      DEFQCBDEFQUDUEUKUJUMACBFRUIULGACUHHACSTUAGHABDEFUBGHCBDEFUBUC $.
      $( [29-May-04] $) $( [17-May-04] $)

    $( Equality theorem for isomorphisms. $)
    isoeq5 $p |- ( B = C ->
          ( H Isom R , S ( A , B ) <-> H Isom R , S ( A , C ) ) ) $=
      ( vx vy wceq wf1o cv wbr cfv wb wral wa wiso f1oeq3 birand df-iso
      3bitr4g ) BCIZABFJZGKZHKZDLUDFMUEFMELNHAOGAOZPACFJZUFPABDEFQACDEFQUBUCUGU
      FBCAFRSGHABDEFTGHACDEFTUA $.
      $( [29-May-04] $) $( [17-May-04] $)
  $}


  ${
    $d y z w H $.  $d y z w R $.  $d y z w S $.  $d y z w A $.  $d y z w B $.
    $d x y z w $.
    hbiso.1 $e |- ( y e. H -> A. x y e. H ) $.
    hbiso.2 $e |- ( y e. R -> A. x y e. R ) $.
    hbiso.3 $e |- ( y e. S -> A. x y e. S ) $.
    hbiso.4 $e |- ( y e. A -> A. x y e. A ) $.
    hbiso.5 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for an isomorphism. $)
    hbiso $p |- ( H Isom R , S ( A , B ) -> A. x H Isom R , S ( A , B ) ) $=
      ( vz vw wf1o cv wbr cfv wb wral wa wal wiso hbf1o wel ax-17 hbel hbbr
      hbfv hbbi hbral hban df-iso bial 3imtr4 ) CDGOZMPZNPZEQZUQGRZURGRZFQZSZNC
      TZMCTZUAZVFAUBCDEFGUCZVGAUBUPVEAABCDGHKLUDVDAMCABBUQCBMUEAUFZKUGVCANCABBU
      RCBNUEAUFZKUGUSVBAABUQUREVHIVIUHABUTVAFABUQGHVHUIJABURGHVIUIUHUJUKUKULMNC
      DEFGUMZVGVFAVJUNUO $.
      $( [29-May-04] $) $( [17-May-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $( An isomorphism is a one-to-one onto function. $)
    isof1o $p |- ( H Isom R , S ( A , B ) -> H : A -1-1-onto-> B ) $=
      ( vx vy wiso wf1o cv wbr cfv wb wral df-iso pm3.26bd ) ABCDEHABEIFJZGJZCK
      QELRELDKMGANFANFGABCDEOP $.
      $( [29-May-04] $) $( [27-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $.  $d x y H $.
    $d x y C $.  $d x y D $.
    $( An isomorphism connects binary relations via its function values. $)
    isorel $p |- ( ( H Isom R , S ( A , B ) /\ ( C e. A /\ D e. A ) ) ->
                 ( C R D <-> ( H ` C ) S ( H ` D ) ) ) $=
      ( vx vy wiso wcel wa wbr cfv wb cv wral wi wf1o df-iso pm3.27bd wceq
      breq1 fveq2 breq1d bi2bid breq2 breq2d rcla42v syl imp ) ABEFGJZCAKDAKLZC
      DEMZCGNZDGNZFMZOZULHPZIPZEMZUSGNZUTGNZFMZOZIAQHAQZUMURRULABGSVFHIABEFGTUA
      VEURCUTEMZUOVCFMZOHICDAAUSCUBZVAVGVDVHUSCUTEUCVIVBUOVCFUSCGUDUEUFUTDUBZVG
      UNVHUQUTDCEUGVJVCUPUOFUTDGUDUHUFUIUJUK $.
      $( [31-May-04] $) $( [27-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    $( Identity law for isomorphism.  Proposition 6.30(1) of [TakeutiZaring]
       p. 33. $)
    isoid $p |- ( I |` A ) Isom R , R ( A , A ) $=
      ( vx vy cid cres wiso wf1o cv wbr cfv wb wral wa f1oi wcel fvres cvv
      wceq visset fvi ax-mp syl6eq breqan12d bicomd rgen2 pm3.2i df-iso mpbir
      ) AABBEAFZGAAUJHZCIZDIZBJZULUJKZUMUJKZBJZLZDAMCAMZNUKUSAOURCDAULAPZUMAPZN
      UQUNUTVAUOULUPUMBUTUOULEKZULULAEQULRPVBULSCTULRUAUBUCVAUPUMEKZUMUMAEQUMRP
      VCUMSDTUMRUAUBUCUDUEUFUGCDAABBUJUHUI $.
      $( [2-May-04] $) $( [27-Apr-04] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w R $.  $d x y z w S $.
    $d x y z w H $.
    $( Converse law for isomorphism.  Proposition 6.30(2) of [TakeutiZaring]
       p. 33. $)
    isocnv $p |- ( H Isom R , S ( A , B ) -> `' H Isom S , R ( B , A ) ) $=
      ( vx vy vz vw wf1o cv wbr cfv wb wral wa ccnv wiso f1ocnv adantr wcel
      wceq f1ocnvfv2 adantrr adantrl breq12d adantlr wf wi ffvrn exp im2and
      breq1 fveq2 breq1d bi2bid pm4.21 syl6bb breq2d breq2 rcla42v sylan9 f1of
      syl sylan imp bitr3d exp32 r19.21adv r19.21aiv jca df-iso 3imtr4 ) ABEJZF
      KZGKZCLZVOEMZVPEMZDLZNZGAOFAOZPZBAEQZJZHKZIKZDLZWFWDMZWGWDMZCLZNZIBOZHBOZ
      PABCDERBADCWDRWCWEWNVNWEWBABESZTWCWMHBWCWFBUAZWLIBWCWPWGBUAZWLWCWPWQPZPWI
      EMZWJEMZDLZWHWKVNWRXAWHNWBVNWRPWSWFWTWGDVNWPWSWFUBWQABWFEUCUDVNWQWTWGUBWP
      ABWGEUCUEUFUGWCWRXAWKNZBAWDUHZWBWRXBUIVNXCWRWIAUAZWJAUAZPWBXBXCWPXDWQXEXC
      WPXDBAWFWDUJUKXCWQXEBAWGWDUJUKULWAXBWSVSDLZWIVPCLZNZFGWIWJAAVOWIUBZWAXGXF
      NXHXIVQXGVTXFVOWIVPCUMXIVRWSVSDVOWIEUNUOUPXGXFUQURVPWJUBZXFXAXGWKXJVSWTWS
      DVPWJEUNUSVPWJWICUTUPVAVBVNWEXCWOBAWDVCVDVEVFVGVHVIVJVKFGABCDEVLHIBADCWDV
      LVM $.
      $( [30-May-04] $) $( [27-Apr-04] $)
  $}

  ${
    $v T $.
    isotr.vT $f class T $.
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u R $.  $d x y z w v u S $.  $d x y z w v u T $.
    $d x y z w v u G $.  $d x y z w v u H $.
    $( Composition (transitive) law for isomorphism.  Proposition 6.30(3) of
       [TakeutiZaring] p. 33. $)
    isotr $p |- ( ( H Isom R , S ( A , B ) /\ G Isom S , T ( B , C ) ) ->
               ( G o. H ) Isom R , T ( A , C ) ) $=
      ( vz vw vv vu vx vy wf1o cv wbr cfv wb wral wa ccom wiso pm3.26 im2an
      ancoms f1oco syl wcel wi wf f1of ffvrn exp im2and adantr wceq breq1
      fveq2 breq1d bi2bid breq2 breq2d rcla42v adantl sylan9 imp weq adantll
      adantlr wfun fvco2 adantrr adantrl breq12d f1ofun sylan 3bitr4d exp32
      r19.21adv r19.21aiv jca df-iso bi2an 3imtr4 ) ABGOZIPZJPZDQZWGGRZWHGRZEQZ
      SZJATIATZUAZBCFOZKPZLPZEQZWQFRZWRFRZHQZSZLBTKBTZUAZUAZACFGUBZOZMPZNPZDQZX
      IXGRZXJXGRZHQZSZNATZMATZUAABDEGUCZBCEHFUCZUAACDHXGUCXFXHXQXFWPWFUAZXHXEWO
      XTXEWPWOWFWPXDUDWFWNUDUEUFZABCFGUGUHXFXPMAXFXIAUIZXONAXFYBXJAUIZXOXFYBYCU
      AZUAXIGRZXJGRZEQZYEFRZYFFRZHQZXKXNXFYDYGYJSZWOYDYEBUIZYFBUIZUAZXEYKWFYDYN
      UJZWNWFABGUKZYOABGULZYPYBYLYCYMYPYBYLABXIGUMUNYPYCYMABXJGUMUNUOUHUPXDYNYK
      UJWPXCYKYEWREQZYHXAHQZSKLYEYFBBWQYEUQZWSYRXBYSWQYEWREURYTWTYHXAHWQYEFUSUT
      VAWRYFUQZYRYGYSYJWRYFYEEVBUVAXAYIYHHWRYFFUSVCVAVDVEVFVGWOYDXKYGSZXEWNYDUV
      BWFWNYDUVBWMUVBXIWHDQZYEWKEQZSIJXIXJAAIMVHZWIUVCWLUVDWGXIWHDURUVEWJYEWKEW
      GXIGUSUTVAJNVHZUVCXKUVDYGWHXJXIDVBUVFWKYFYEEWHXJGUSVCVAVDVGVIVJXTYDXNYJSZ
      XFFVKZYPUAZYDUVGXTUVIYDUAXLYHXMYIHUVIYBXLYHUQYCABXIFGVLVMUVIYCXMYIUQYBABX
      JFGVLVNVOWPUVHWFYPBCFVPYQUEVQYAVQVRVSVTWAWBXRWOXSXEIJABDEGWCKLBCEHFWCWDMN
      ACDHXGWCWE $.
      $( [13-Jun-04] $) $( [27-Apr-04] $)
  $}

  ${
    $v T $.
    isotr.vT $f class T $.
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w R $.
    $d x y z w S $.  $d x y z w T $.  $d x y z w G $.  $d x y z w H $.
    $( Composition (transitive) law for isomorphism.  Proposition 6.30(3) of
       [TakeutiZaring] p. 33.  This proof is shorter in set.mm but takes 240K
       vs. 207K for the web page. $)
    isotrALT $p |- ( ( H Isom R , S ( A , B ) /\ G Isom S , T ( B , C ) ) ->
               ( G o. H ) Isom R , T ( A , C ) ) $=
      ( vx vy vz vw wf1o cv wbr cfv wb wral wa ccom wiso pm3.26 im2an ancoms
      f1oco syl ax-17 hbra1 hban wcel hbral wi wf f1of ffvrn exp im2and adantr
      wceq breq1 fveq2 breq1d bi2bid breq2 breq2d rcla42v adantl sylan9 imp
      ra42 adantll adantlr wfun fvco2 adantrr adantrl breq12d f1ofun sylan
      3bitr4d exp32 r19.21ad r19.21ai jca df-iso bi2an 3imtr4 ) ABGMZINZJNZDOZW
      IGPZWJGPZEOZQZJARZIARZSZBCFMZKNZLNZEOZWTFPZXAFPZHOZQZLBRKBRZSZSZACFGTZMZW
      KWIXJPZWJXJPZHOZQZJARZIARZSABDEGUAZBCEHFUAZSACDHXJUAXIXKXQXIWSWHSZXKXHWRX
      TXHWSWRWHWSXGUBWHWQUBUCUDZABCFGUEUFXIXPIAWRXHIWHWQIWHIUGWPIAUHUIXHIUGUIXI
      WIAUJZXOJAWRXHJWHWQJWHJUGWPJIAYBJUGZWOJAUHUKUIXHJUGUIYCXIYBWJAUJZXOXIYBYD
      SZSWNWLFPZWMFPZHOZWKXNXIYEWNYHQZWRYEWLBUJZWMBUJZSZXHYIWHYEYLULZWQWHABGUMZ
      YMABGUNZYNYBYJYDYKYNYBYJABWIGUOUPYNYDYKABWJGUOUPUQUFURXGYLYIULWSXFYIWLXAE
      OZYFXDHOZQKLWLWMBBWTWLUSZXBYPXEYQWTWLXAEUTYRXCYFXDHWTWLFVAVBVCXAWMUSZYPWN
      YQYHXAWMWLEVDYSXDYGYFHXAWMFVAVEVCVFVGVHVIWRYEWOXHWQYEWOWHWQYEWOWOIJAAVJVI
      VKVLXTYEXNYHQZXIFVMZYNSZYEYTXTUVBYESXLYFXMYGHUVBYBXLYFUSYDABWIFGVNVOUVBYD
      XMYGUSYBABWJFGVNVPVQWSUVAWHYNBCFVRYOUCVSYAVSVTWAWBWCWDXRWRXSXHIJABDEGWEKL
      BCEHFWEWFIJACDHXJWEWG $.
      $( [13-Jun-04] $) $( [27-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $. $d x y H $.  $d x y C $.
    $d x y D $.
    $( Isomorphisms preserve minimal elements.  Note that ` ( ``' R " { D } ) `
       is Takeuti and Zaring's idiom for the initial segment
       ` { x | x R D } ` .  Proposition 6.31(1) of [TakeutiZaring] p. 33. $)
    isomin $p |- ( ( H Isom R , S ( A , B ) /\ ( C (_ A /\ D e. A ) ) ->
               ( ( C i^i ( `' R " { D } ) ) = (/) <->
               ( ( H " C ) i^i ( `' S " { ( H ` D ) } ) ) = (/) ) ) $=
      ( vy vx wiso wss wcel wa ccnv csn cima cin c0 wceq cfv cv wex wn wrex
      wbr wb ssel wf1o wfn wi isof1o f1ofn visset fnfvbr exp 3syl syl9r imp31
      birexdva elima syl6rbbr cvv fvex eliniseg ax-mp a1i bi2and elin r19.41v
      3bitr4g adantrr ad2antrr isorel bitrd breq1 biimpar syl5bir exp32 com34
      imp32 r19.22dv sylbid biex n0 df-rex 3bitr4 syl6ibr 19.23adv syl5ib a3d
      com12 funfvima funfni com13 syld imp syl sylan adantrd biimpd imp3a jcad
      3imtr4g noel2 syl6 impbid ) ABEFGJZCAKZDALZMZMZCENDOPZQZRSZGCPZFNDGTZOPZQ
      ZRSZXKXSXNXKHUAZXRLZHUBXNUCZXSUCZXKYAYBHXKYAIUAZXLLZICUDZYBXKYAYDGTZXTSZX
      TXPFUEZMZICUDZYFXGXHYAYKUFXIXGXHMZXTXOLZXTXQLZMYHICUDZYIMYAYKYLYMYOYNYIYL
      YOYDXTGUEZICUDYMYLYHYPICXGXHYDCLZYHYPUFZXHYQYDALZXGYRCAYDUGZXGABGUHZGAUIZ
      YSYRUJABEFGUKZABGULZUVBYSYRAYDXTGHUMZUNUOUPUQURUSIXTGCUVEUTVAYNYIUFZYLXPV
      BLZUVFDGVCZFXPXTVBUVEVDVEVFVGXTXOXQVHYHYIICVIVJVKXKYJYEICXGXHXIYQYJYEUJZU
      JXGXHYQXIUVIXHYQYSXGXIUVIUJYTXGYSXIUVIXGYSXIMMZYEYGXPFUEZYJUVJYEYDDEUEZUV
      KXIYEUVLUFXGYSEDYDAIUMVDVLZABYDDEFGVMZVNYHUVKYIYGXTXPFVOVPVQVRUQVSVTWAWBY
      DXMLZIUBZYQYEMZIUBYBYFUVOUVQIYDCXLVHZWCIXMWDZYEICWEWFWGWHHXRWDWIWJXKXNXSX
      KUVPYCYBXKUVOYCIXKUVOYGXRLZYCXKUVQYGXOLZYGXQLZMUVOUVTXKUVQUWAUWBXKYQUWAYE
      UVBXJYQUWAUJZXGUVBXHUWCXIUVBXHUWCYQXHUVBUWAYQXHYSUVBUWAUJXHYQYSYTWKUVBYSY
      QUWAUVBYSUWCUWCAYDGCYDGWLWMUOWNWOWNWPVKXGUVAUVBUVCUVDWQWRWSXKYQYEUWBXGXHX
      IYQYEUWBUJZUJXGXHYQXIUWDXHYQYSXGXIUWDUJYTXGYSXIUWDUVJYEUVLUWBUVMUVJUVLUVK
      UWBUVJUVLUVKUVNWTUVGUWBUVKUFUVHFXPYGVBYDGVCVDVEWGWBVRUQVSVTXAXBUVRYGXOXQV
      HXCXRYGXDXEWHUVSWIWJXF $.
      $( [7-Jun-04] $) $( [19-Apr-04] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.  $d x y S $. $d x y H $.  $d x y D $.
    $( Isomorphisms preserve initial segments.  Proposition 6.31(2) of
       [TakeutiZaring] p. 33. $)
    isoini $p |- ( ( H Isom R , S ( A , B ) /\ D e. A ) ->
               ( H " ( A i^i ( `' R " { D } ) ) ) =
               ( B i^i ( `' S " { ( H ` D ) } ) ) ) $=
      ( vx vy wiso wcel wa ccnv cfv csn cima cin cv wbr wrex cab wceq wb crn
      wf1o wfo isof1o f1ofo forn eleq2d 3syl wfn f1ofn fvelrn bitr3d cvv fvex
      visset eliniseg ax-mp a1i bi2and adantr biland elin syl5bb birand anass
      syl6bb adantl wi isorel fnfvbr bicomd syl sylan adantrr ancom breq1
      pm5.32i bitr exp32 com23 imp pm5.32d bitrd birexdv2 r19.41v bitr4d
      biabrdv dfima2 syl6reqr ) ABDEFIZCAJZKZBELCFMZNOZPZGQZHQZFRZGADLCNOZPZSZH
      TFXBOWNXCHWQWNWSBJZWSWPJZKZXCWSWQJWNXFWRFMZWSUAZGASZWSWOERZKZXCWLXFXKUBWM
      WLXDXIXEXJWLWSFUCZJZXDXIWLABFUDZABFUEZXMXDUBABDEFUFZABFUGXOXLBWSABFUHUIUJ
      WLXNFAUKZXMXIUBXPABFULZGAWSFUMUJUNXEXJUBZWLWOUOJXSCFUPEWOWSUOHUQZURUSUTVA
      VBWNXCXHXJKZGASXKWNWTYAGXBAWNWRXBJZWTKZWRAJZWRCDRZWTKZKZYDYAKWMYCYGUBWLWM
      YCYDYEKZWTKYGWMYBYHWTWMYDWRXAJZKYHYBWMYIYEYDDCWRAGUQURVCWRAXAVDVEVFYDYEWT
      VGVHVIWNYDYFYAWLWMYDYFYAUBZVJWLYDWMYJWLYDWMYJWLYDWMKKZYFXGWOERZXHKZYAYKYE
      YLWTXHABWRCDEFVKWLYDWTXHUBZWMXQYDYNWLXQYDKXHWTAWRWSFXTVLVMWLXNXQXPXRVNVOV
      PVAYMXHYLKYAYLXHVQXHYLXJXGWSWOEVRVSVTVHWAWBWCWDWEWFXHXJGAWGVHWHWSBWPVDVEW
      IGHFXBWJWK $.
      $( [17-Jun-04] $) $( [20-Apr-04] $)
  $}

  ${
    $d x y w A $.  $d x y z w B $.  $d x y w R $.  $d x y z w S $.
    $d x y z w H $.
    $( Lemma for ~ isofr . $)
    isofrlem $p |- ( H Isom R , S ( A , B ) -> ( S Fr B -> R Fr A ) ) $=
      ( vx vy vw vz wiso wfr cv wss c0 wceq wn wa ccnv csn cima cin wrex wi
      wal wf1o wfun isof1o f1ofun cvv wcel visset funimaex sseq1 cleq1 binegd
      bi2and ineq1 cleq1d rexeqd bi2imd cla4gv syl 3syl dffr3 syl5ib wfo f1ofo
      crn imassrn forn sseq2d mpbii a1d wfn f1ofn wel wex ssel cfv funfvima
      funfni noel2 syl6 exp sylan9r pm2.43d 19.23adv n0 imp3a jcad syl5d
      fvelima adantr pm3.26 syl2an wb isomin imdistani sylan5 sneq imaeq2
      ineq2d sylan9bb pm3.27 syl5bir exp42 imp com3l com4t r19.22dv mpd exp32
      r19.23adv adantrd a2d syld 19.21adv syl6ibr ) ABCDEJZBDKZFLZAMZYANOPZQZYA
      CRGLZSTUANOZGYAUBZUCZFUDACKXSXTYHFXSXTYDEYATZDRZHLZSZTZUAZNOZHYIUBZUCYHXS
      XTYIBMZYINOZPZQZYPYDXSILZBMZUVANOZPZQZUVAYMUAZNOZHUVAUBZUCZIUDZYTYPUCZXTX
      SABEUEZEUFZUVJUVKUCZABCDEUGZABEUHZUVMYIUIUJUVNEYAFUKULUVIUVKIYIUIUVAYIOZU
      VEYTUVHYPUVQUVBYQUVDYSUVAYIBUMUVQUVCYRUVAYINUNUOUPUVGYOHUVAYIUVQUVFYNNUVA
      YIYMUQURUSUTVAVBVCIHBDVDVEXSUVLYDYTUCUVOUVLYDYQYSUVLYQYDUVLABEVFZYQABEVGU
      VRYIEVHZMYQEYAVIUVRUVSBYIABEVJVKVLVBVMUVLEAVNZYDYSUCABEVOUVTYBYCYSUVTYBYC
      YSUCUVTYBQZGFVPZGVQYSYCUWAUWBYSGUWAUWBYSYBUWBYEAUJZUVTUWBYSUCZYAAYEVRZUVT
      UWCUWDUVTUWCQUWBYEEVSZYIUJZYSUWBUWGUCAYEEYAYEEVTWAYIUWFWBWCWDWEWFWGGYAWHV
      EWDWIVBWJVBWKXSYDYPYGXSYBYPYGUCZYCXSYBUWHXSYBQZYOYGHYIUWIYKYIUJZYOYGUWIUW
      JYOQZQZUWFYKOZGYAUBZYGUVMUWJUWNUWIUWKGYKYAEWLUWIUVLUVMXSUVLYBUVOWMUVPVBUW
      JYOWNWOUWLUWMYFGYAUWIUWKUWBUWMYFUCUCUWBUWMUWIUWKYFUWIUWBUWMUWKYFUCZXSYBUW
      BUWMUWOUCUCXSYBUWBUWMUWOXSYBUWBQZQZUWMQYFYOUWKUWQYFYIYJUWFSZTZUAZNOZUWMYO
      XSYBUWCQYFUXAWPUWPABYAYECDEWQYBUWBUWCUWEWRWSUWMUWTYNNUWMUWSYMYIUWMUWRYLOU
      WSYMOUWFYKWTUWRYLYJXAVBXBURXCUWJYOXDXEXFXGXHXIXGXJXKXLXMWDXNXOXPXQFGACVDX
      R $.
      $( [8-Jun-04] $) $( [29-Apr-04] $)
  $}

  $( An isomorphism preserves foundedness.  Proposition 6.32(1) of
     [TakeutiZaring] p. 33. $)
  isofr $p |- ( H Isom R , S ( A , B ) -> ( R Fr A <-> S Fr B ) ) $=
    ( wiso wfr ccnv wi isocnv isofrlem syl impbid ) ABCDEFZACGZBDGZNBADCEHZFOPI
    ABCDEJBADCQKLABCDEKM $.
    $( [8-Jun-04] $) $( [30-Apr-04] $)

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y R $.  $d x y z w S $.
    $d x y z w H $.
    $( An isomorphism preserves well ordering.  Proposition 6.32(3) of
       [TakeutiZaring] p. 33. $)
  isowe $p |- ( H Isom R , S ( A , B ) -> ( R We A <-> S We B ) ) $=
    ( vx vy vz vw wiso wfr cv wbr weq w3o wral wa wwe isofr cfv wceq wb wcel
    isorel wf1 f1fveq wf1o isof1o f1of1 syl sylan bicomd ancom sylan5b bi3ord
    exp32 r19.21adv r19.15 syl6 r19.21aiv wfo f1ofo breq2 cleq2 breq1 cbvfo
    biraldv cleq1 bitrd 3syl bi2and dfwe2 3bitr4g ) ABCDEJZACKZFLZGLZCMZFGNZVQV
    PCMZOZGAPZFAPZQBDKZHLZILZDMZHINZWFWEDMZOZIBPZHBPZQACRBDRVNVOWDWCWLABCDESVNW
    CVPETZVQETZDMZWMWNUAZWNWMDMZOZGAPZFAPZWLVNWBWSUBZFAPWCWTUBVNXAFAVNVPAUCZWAW
    RUBZGAPXAVNXBXCGAVNXBVQAUCZXCVNXBXDQZQZVRWOVSWPVTWQABVPVQCDEUDXFWPVSABEUEZX
    EWPVSUBVNABVPVQEUFVNABEUGZXGABCDEUHZABEUIUJUKULVNXDXBQVTWQUBXEABVQVPCDEUDXB
    XDUMUNUOUPUQWAWRGAURUSUTWBWSFAURUJVNXHABEVAZWTWLUBXIABEVBXJWTWMWFDMZWMWFUAZ
    WFWMDMZOZIBPZFAPWLXJWSXOFAWRXNGIABEWNWFUAWOXKWPXLWQXMWNWFWMDVCWNWFWMVDWNWFW
    MDVEUOVFVGXOWKFHABEWMWEUAZXNWJIBXPXKWGXLWHXMWIWMWEWFDVEWMWEWFVHWMWEWFDVCUOV
    GVFVIVJVIVKFGACVLHIBDVLVM $.
    $( [12-Jun-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d x y z w v u A $.  $d x y v u B $.  $d x y z w v u H $.
    $d x y z w v u R $.  $d v u S $.
    $( Any one-to-one onto function determines an isomorphism with an
       induced relation ` S ` .  Proposition 6.33 of [TakeutiZaring] p. 34. $)
    f1oiso $p |- ( ( H : A -1-1-onto-> B /\ S = { <. z , w >. |
     E. x e. A E. y e. A ( ( z = ( H ` x ) /\ w = ( H ` y ) ) /\ x R y ) } ) ->
                  H Isom R , S ( A , B ) ) $=
      ( vv vu wf1o cv cfv wceq wa wbr wrex copab wb wral wiso pm3.26 wf1 wcel
      cop eleq2 weq f1fveq cleqcom syl6bb anassrs birand anass syl5bb birexdv
      r19.42v birexdva breq1 biland ceqsrexv adantl bitrd breq2 sylan9bb
      anandis fvex cleq1 opelopab sylan9bbr an1rs df-br syl5rbb exp32
      r19.21adv r19.21aiv f1of1 sylan jca df-iso sylibr ) EFILZHCMZAMZINZOZDMZB
      MZINZOZPZWDWHGQZPZBERZAERZCDSZOZPZWBJMZKMZGQZWSINZWTINZHQZTZKEUAZJEUAZPEF
      GHIUBWRWBXGWBWQUCEFIUDZWQXGWBXHWQPZXFJEXIWSEUEZXEKEXIXJWTEUEZXEXIXJXKPZPX
      BXCUFZHUEZXAXDXHXLWQXNXATWQXNXMWPUEZXHXLPZXAHWPXMUGXPXBWEOZXCWIOZPZWLPZBE
      RZAERZXAXOXHXJXKYBXATXHXJPZYBXRWSWHGQZPZBERZXHXKPZXAYCYBAJUHZXRWLPZBERZPZ
      AERZYFYCYAYKAEYCWDEUEZPZYAYHYIPZBERYKYNXTYOBEYNXQYIPYOXTYNXQYHYIXHXJYMXQY
      HTXHXJYMPPXQJAUHYHEFWSWDIUIWSWDUJUKULUMXQXRWLUNUOUPYHYIBEUQUKURXJYLYFTXHY
      JYFAWSEYHYIYEBEYHWLYDXRWDWSWHGUSUTUPVAVBVCYGYFBKUHZYDPZBERZXAYGYEYQBEYGWH
      EUEZPXRYPYDXHXKYSXRYPTXHXKYSPPXRKBUHYPEFWTWHIUIWTWHUJUKULUMURXKYRXATXHYDX
      ABWTEWHWTWSGVDVAVBVCVEVFWOXQWJPZWLPZBERZAERYBCDXBXCWSIVGWTIVGWCXBOZWNUVBA
      EUVCWMUVABEUVCWKYTWLUVCWFXQWJWCXBWEVHUMUMUPUPWGXCOZUVBYAAEUVDUVAXTBEUVDYT
      XSWLUVDWJXRXQWGXCWIVHUTUMUPUPVIUOVJVKXBXCHVLVMVNVOVPEFIVQVRVSJKEFGHIVTWA
      $.
      $( [21-Jun-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d z w R $.  $d x y z w S $.  $d z w A $.  $d z w B $.  $d x y z w F $.
    f1owe.1 $e |- R = { <. x , y >. | ( F ` x ) S ( F ` y ) } $.
    $( Well-ordering of isomorphic relations. $)
    f1owe $p |- ( F : A -1-1-onto-> B -> ( S We B -> R We A ) ) $=
      ( vz vw wf1o wwe cv wbr cfv wb wral weq fveq2 breq1d breq2d brabg rgen2
      wa wiso df-iso isowe sylbir mpan2 biimprd ) CDGKZCELZDFLZUKIMZJMZENUNGOZU
      OGOZFNZPZJCQICQZULUMPZUSIJCAMZGOZBMZGOZFNUPVEFNURABUNUOCCEAIRVCUPVEFVBUNG
      STBJRVEUQUPFVDUOGSUAHUBUCUKUTUDCDEFGUEVAIJCDEFGUFCDEFGUGUHUIUJ $.
      $( [13-Jun-04] $) $( [4-Mar-97] $)
  $}

  ${
    $d z w v u f R $.  $d x y z w v u f S $.  $d z w v u f A $.
    $d z w v u f B $.  $d x y z w v u f F $.  $( ` z w v u f ` are dummy. $)
    f1owe.1 $e |- R = { <. x , y >. | ( F ` x ) S ( F ` y ) } $.
    $( Well-ordering of isomorphic relations. $)
    f1oweOLD $p |- ( F : A -1-1-onto-> B -> ( S We B -> R We A ) ) $=
      ( vu vf vw vv vz wf1o wfr cv wbr weq w3o wral wa wwe wfo wi wf1 df-f1o
      pm3.27bd wfn crn wceq df-fo freq2 biimprd wfun cdm df-fn wss c0 wn wrex
      wal cima cvv wcel visset funimaex sseq1 cleq1 binegd bi2and raleq rexeqd
      bi2imd cla4gv wel wex cfv funfvima2 noel2 syl6 19.23adv n0 syl5ib imp
      imassrn jctil syl7 syl df-fr com23 exp3a anabsi5 imp3a cres wb fores
      breq1 cbvfo birexdv breq2 biraldv cbvexfo bitrd fvres breqan12rd fveq2
      breq1d breq2d brab syl6rbbr biraldva birexa syl5bb sylibrd exp4b com34
      imp4a 19.21adv syl6ibr biimpd sylan9 sylbi sylan9r a1i f1fveq bicomd
      bi3ord bi2raldva cleq2 sylan9bb im2and dfwe2 3imtr4g ) CDGNZDFOZIPZJPZFQZ
      IJRZYQYPFQZSZJDTZIDTZUACEOZKPZLPZEQZKLRZUVFUVEEQZSZLCTKCTZUADFUBCEUBYNYOU
      VDUVCUVKYNCDGUCZYOUVDUDZYNCDGUEZUVLCDGUFZUGUVLGCUHZGUIZDUJZUAUVMCDGUKUVRY
      OUVQFOZUVPUVDUVRUVSYOUVQDFULUMUVPGUNZGUOZCUJZUAUVSUVDUDGCUPUVTUVSUWAEOZUW
      BUVDUVTUVSMPZUWAUQZUWDURUJUSZUAUVIUSZLUWDTZKUWDUTZUDZMVAUWCUVTUVSUWJMUVTU
      VSUWEUWFUWIUVTUWEUVSUWFUWIUDUVTUWEUWFUVSUWIUVTUWEUWFUVSUWIUVTUWEUAZUWFUVS
      UAYTUSZJGUWDVBZTZIUWMUTZUWIUWKUWFUVSUWOUVTUWEUWFUVSUWOUDZUDUVTUWKUWFUWPUV
      TUVSUWKUWFUAZUWOUVTUVEUVQUQZUVEURUJZUSZUAZUWLJUVETZIUVEUTZUDZKVAZUWQUWOUD
      ZUVSUVTUWMVCVDZUXEUXFUDGUWDMVEVFUXGUXEUWMUVQUQZUWMURUJZUSZUAZUWOUWQUXDUXK
      UWOUDKUWMVCUVEUWMUJZUXAUXKUXCUWOUXLUWRUXHUWTUXJUVEUWMUVQVGUXLUWSUXIUVEUWM
      URVHVIVJUXBUWNIUVEUWMUWLJUVEUWMVKVLVMVNUWQUXJUXHUWKUWFUXJUWKKMVOZKVPUXJUW
      FUWKUXMUXJKUWKUXMUVEGVQZUWMVDUXJUWDUVEGVRUWMUXNVSVTWAKUWDWBWCWDGUWDWEWFWG
      WHKIJUVQFWIWCWJWKWLWMUWKUWDUWMGUWDWNZUCZUWIUWOWOUWDGWPUXPUVFUXOVQZUVEUXOV
      QZFQZUSZLUWDTZKUWDUTZUWOUWIUXPUYBYQUXRFQZUSZJUWMTZKUWDUTUWOUXPUYAUYEKUWDU
      XTUYDLJUWDUWMUXOUXQYQUJUXSUYCUXQYQUXRFWQVIWRWSUYEUWNKIUWDUWMUXOUXRYPUJZUY
      DUWLJUWMUYFUYCYTUXRYPYQFWTVIXAXBXCUWHUYAKUWDUXMUWGUXTLUWDUXMLMVOZUAZUVIUX
      SUYHUXSUVFGVQZUXNFQZUVIUYGUXMUXQUYIUXRUXNFUVFUWDGXDUVEUWDGXDXEAPZGVQZBPZG
      VQZFQZUYIUYNFQUYJABUVFUVEELVEZKVEZALRUYLUYIUYNFUYKUVFGXFXGBKRUYNUXNUYIFUY
      MUVEGXFXHHXIZXJVIXKXLXMWHXNXOXPWJXQXRMKLUWAEWIXSUWBUWCUVDUWACEULXTYAYBYCY
      BWHYNUVKUVCYNUVNUVLUAUVKUVCWOUVOUVNUVKUXNUYIFQZUXNUYIUJZUYJSZLCTZKCTZUVLU
      VCUVNUVJVUAKLCCUVNUVECVDUVFCVDUAUAZUVGUYSUVHUYTUVIUYJUVGUYSWOVUDUYOUXNUYN
      FQUYSABUVEUVFEUYQUYPAKRUYLUXNUYNFUYKUVEGXFXGBLRUYNUYIUXNFUYMUVFGXFXHHXIYD
      VUDUYTUVHCDUVEUVFGYEYFUVIUYJWOVUDUYRYDYGYHUVLVUCYPUYIFQZYPUYIUJZUYIYPFQZS
      ZLCTZIDTUVCVUBVUIKICDGUXNYPUJZVUAVUHLCVUJUYSVUEUYTVUFUYJVUGUXNYPUYIFWQUXN
      YPUYIVHUXNYPUYIFWTYGXAWRUVLVUIUVBIDVUHUVALJCDGUYIYQUJVUEYRVUFYSVUGYTUYIYQ
      YPFWTUYIYQYPYIUYIYQYPFWQYGWRXAXCYJYBUMYKIJDFYLKLCEYLYM $.
      $( [4-Mar-97] $)
  $}


