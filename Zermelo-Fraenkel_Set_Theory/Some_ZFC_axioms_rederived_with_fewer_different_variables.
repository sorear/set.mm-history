$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some ZFC axioms rederived with fewer different variables.
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u t $.

    $( Axiom of Union expressed with fewest number of different variables. $)
    axun  $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw wel wa wex wi wal ax-un weq a14b a13b bi2and cbvexv birim bial biex
      mpbi ) BDEZDCEZFZDGZBAEZHZBIZAGUDACEZFZAGZUDHZBIZAGCABDJUFUKAUEUJBUCUIUDU
      BUHDADAKTUDUAUGDABLDACMNOPQRS $.
      $([14-Aug-03]$)

    $( Axiom of Power Sets expressed with fewest number of different
       variables. $)
    axpow $p |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) $=
      ( vw wel wi wal wex ax-pow weq a13b bi2imd cbvalv birim bial biex mpbi )
      DBEZDCEZFZDGZBAEZFZBGZAHABEZACEZFZAGZUBFZBGZAHCABDIUDUJAUCUIBUAUHUBTUGDAD
      AJRUESUFDABKDACKLMNOPQ $.
      $([14-Aug-03]$)

    $( Axiom of Regularity expressed more compactly. $)
    axreg $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( wel wn wi wal wa wex ax-reg 19.23bi ) ABDZLCADCBDEFCGHAIABACJK $.
      $([14-Aug-03]$)

    $( Axiom of Infinity expressed with fewest number of different
       variables.  $)
    axinf $p |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( vw wel wa wex wi wal ax-inf weq a13b birand biexdv bi2imd cbvalv bilan
      biex mpbi ) BAEZDAEZDCEZCAEZFZCGZHZDIZFZAGTTBCEZUCFZCGZHZBIZFZAGBADCJUHUN
      AUGUMTUFULDBDBKZUATUEUKDBALUOUDUJCUOUBUIUCDBCLMNOPQRS $.
      $([14-Aug-03]$)

    $( Axiom of Choice expressed with fewest number of different variables. $)
    axac $p  |- E. x A. y A. z ( ( y e. z /\ z e. w ) -> E. w A. y ( E. w
              ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $=
      ( vu vt vv wel wa wex weq wb wal wi ax-ac eqt2b bilbid a14b biland a13b
      bi2and cbvexv birbi syl6bb bialdv birand biexdv a8b bi2bid cbvalv bilim
      bi2al biex mpbi ) BCHZCDHZIZECHZCFHZIZEFHZFAHZIZIZFJZEGKZLZEMZGJZNZCMBMZA
      JUQUQBDHZDAHZIZIZDJZBDKZLZBMZDJZNZCMBMZAJDABCGEFOVKWBAVJWABCVIVTUQVHVSGDG
      DKZVHURUPIZEDHZVMIZIZDJZEDKZLZEMVSWCVGWJEWCVGVEWILWJWCVFWIVEGDEPQVEWHWIVD
      WGFDFDKZUTWDVCWFWKUSUPURFDCRSWKVAWEVBVMFDERFDATUAUAUBUCUDUEWJVREBEBKZWHVP
      WIVQWLWGVODWLWDUQWFVNWLURUOUPEBCTUFWLWEVLVMEBDTUFUAUGEBDUHUIUJUDUBUKULUMU
      N $.
      $([14-Aug-03]$)
  $}


