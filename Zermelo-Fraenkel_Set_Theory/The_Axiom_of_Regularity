$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Regularity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Axiom of Regularity (variation) $)
    axreg $p |- ( E. x x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( wel wn wi wal wa wex hbe1 ax-reg 19.23ai ) ABDZMCADCBDEFCGHZAIANAJABCKL
      $.
      $([2-Jan-97]$)
  $}

  ${
    $d x y A z $.  $( ` z ` is dummy. $)
    zfregclw.1 $e |- A e. V $.
    $( The Axiom of Regularity with class variables. $)
    zfregclw   $p |- ( E. x x e. A ->
                     E. x ( x e. A /\ A. y ( y e. x -> -. y e. A ) ) ) $=
      ( vz wel wex wn wi wal wa cv wcel wceq eleq2 biexdv binegd bilimd bialdv
      bi2and bi2imd axreg vtoclbv ) AEFZAGZUDBAFZBEFZHZIZBJZKZAGZIALZCMZAGZUNUF
      BLZCMZHZIZBJZKZAGZIECDELZCNZUEUOULVBVDUDUNAVCCUMOZPVDUKVAAVDUDUNUJUTVEVDU
      IUSBVDUHURUFVDUGUQVCCUPOQRSTPUAAEBUBUC $.
      $([5-Aug-94]$)
  $}

  ${
    $d x A y $.  $( ` y ` is dummy. $)
    zfregwOBS.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  Axiom 6 of Takeuti and
       Zaring, p. 21.  This is called the 'weak form'; there is also a
       'strong form', not requiring that ` A ` be a set, that can be proved
       (with more difficulty).  $)
    zfregwOBS   $p |- ( -. A = (/) -> E. x ( x e. A /\ ( x i^i A ) = (/) ) ) $=
      ( vy cv wcel wex wel wn wi wal wa c0 wceq cin zfregclw nnull disj bilan
      biex 3imtr4 ) AEZBFZAGUCDAHDEBFIJDKZLZAGBMNIUCUBBOMNZLZAGADBCPABQUGUEAUFU
      DUCDUBBRSTUA $.
      $([5-Aug-94]$)
  $}

  ${
    $d x A y $.  $( ` y ` is dummy. $)
    $( The Axiom of Regularity using abbreviations (with class existence
       as antecedent).  $)
    zfregwg   $p |- ( A e. B -> ( -. A = (/) ->
                      E. x ( x e. A /\ ( x i^i A ) = (/) ) ) ) $=
      ( vy cv c0 wceq wn wel cin wa wex wi wcel cleq1 binegd eleq2 ineq2
      cleq1d bi2and biexdv bi2imd visset zfregwOBS vtoclg ) DEZFGZHZADIZAEZUFJZ
      FGZKZALZMBFGZHZUJBNZUJBJZFGZKZALZMDBCUFBGZUHUPUNVAVBUGUOUFBFOPVBUMUTAVBUI
      UQULUSUFBUJQVBUKURFUFBUJRSTUAUBAUFDUCUDUE $.
      $([29-Oct-96]$)
  $}

  ${
    $d x A y $.  $( ` y ` is dummy. $)
    zfregw.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  Axiom 6 of Takeuti and
       Zaring, p. 21.  This is called the 'weak form'; there is also a
       'strong form', not requiring that ` A ` be a set, that can be proved
       (with more difficulty).  $)
    zfregw   $p |- ( -. A = (/) -> E. x ( x e. A /\ ( A i^i x ) = (/) ) ) $=
      ( vy cv wcel wex wel wn wi wal wa c0 wceq cin zfregclw nnull disj bi2.03
      bial bitr bilan biex 3imtr4 ) AEZBFZAGUFDAHZDEBFZIJZDKZLZAGBMNIUFBUEOMNZL
      ZAGADBCPABQUMUKAULUJUFULUHUGIJZDKUJDBUERUNUIDUHUGSTUAUBUCUD $.
      $([26-Nov-95]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The epsilon relation is founded on any class. $)
    zfregfr $p |- E Fr A $=
      ( vx vy cep wfr cv wss c0 wceq wn wa wel cin wex wi dfepfr visset zfregw
      adantl mpgbir ) ADEBFZAGZUAHIJZKCBLUACFMHIKCNZOBBCAPUCUDUBCUABQRST $.
      $([26-Nov-95]$)
  $}

  ${
    $d x y z $.  $( ` y z ` are dummy. $)
    $( No set is a member of itself.  Theorem 105, p. 54 of Suppes.  (This
       is trivial to prove from ~zfregfr and ~efrirr , but this proof is
       direct from the Axiom of Regularity like the one in Suppes.) $)
    eirrv $p |- -. x e. x $=
      ( vy vz wel cv csn wcel wn wi wal wa wex weq ax-14 eqcoms comm elsn
      syl5ib a13b eleq1 binegd bi2imd visset snid mt2bi syl6bbr a4b1 nsyli
      pm2.03d 19.21aiv alnex sylib a4w1 snex zfregclw ax-mp mt2 ) AADZBEZAEZFZG
      ZCBDZCEZVAGZHZIZCJZKBLZURVBVHHIZBJVIHURVJBURVHVBURVBABDZVHURBAMZVKVBVLURV
      KURVKIABABANOPBUTQRVGVKHZCACAMZVGVKUTVAGZHZIVMVNVCVKVFVPCABSVNVEVOVDUTVAT
      UAUBVOVKUTAUCUDZUEUFUGUHUIUJVBVHBUKULVBBLVIVBVOBAUSUTVATVQUMBCVAUTUNUOUPU
      Q $.
      $([19-Aug-93]$)
  $}

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( No class is a member of itself.  Exercise 6, p. 22, of Takeuti and
       Zaring. $)
    eirr $p |- -. A e. A $=
      ( vx wcel wn wi wel cv wceq eleq2 eleq1 bitrd binegd eirrv vtoclg pm2.01
      ax-mp ) AACZQDZERBBFZDRBAABGZAHZSQUASTACQTATITAAJKLBMNQOP $.
      $([7-Aug-94]$)
  $}

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    $( No class has 2-cycle membership loops.  Theorem 7X(b) of Enderton,
       p. 206. $)
    en2lp $p |- -. ( A e. B /\ B e. A ) $=
      ( vx vy cvv wcel wa wn wel cv wceq eleq1 eleq2 bi2and binegd cep wfr
      efrn2lp zfregfr visset pm3.2i mp2an vtocl2i elisset im2an con3 pm2.61i )
      AEFZBEFZGZABFZBAFZGZHZCDIZDCIZGZHZADJZFZUSAFZGZHUNCDABEECJZAKZUQVBVDUOUTU
      PVAVCAUSLVCAUSMNOUSBKZVBUMVEUTUKVAULUSBAMUSBALNOEPQVCEFZUSEFZGURCDERESVFV
      GCTDTUAUBUCUMUJUKUHULUIABUDBAUDUEUFUG $.
      $([16-Oct-96]$)
  $}

  ${
    preleq.1 $e |- A e. V $.
    preleq.2 $e |- B e. V $.
    preleq.3 $e |- C e. V $.
    preleq.4 $e |- D e. V $.
    $( Equality of two unordered pairs (special case). $)
    preleq   $p |- ( ( ( A e. B /\ C e. D ) /\ { A , B } = { C , D } ) ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wcel wa wn wo prer12 biimp ord eleq12 birand en2lp mtbiri
      syl6 a3d imp ancoms ) ABICDIJZABKZCDKZLZACJBDJLZUFUIUJUFUJUIUFUJMADJBCJLZ
      UIMUFUJUKUFUJUKNABCDEFGHOPQUKUIDCKZUHLUKUGULUHADBCRSDCTUAUBUCUDUE $.
      $([16-Oct-96]$)

    $( Theorem for alternate representation of ordered pairs, requiring
       Regularity.  Exercise 34 of Enderton, p. 207. $)
    opthreg   $p |- ( { A , { A , B } } = { C , { C , D } } ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wa wcel zfpaircl preleq pri1 pm3.2i mpan pm3.26 preq1 cleq1d
      prer2 syl6bi imp jca syl ) AABIZICCDIZIJZACJZUFUGJZKZUIBDJZKAUFLZCUGLZKUH
      UKAUFCUGEABMGCDMNUMUNABEOCDGOPQUKUIULUIUJRUIUJULUIUJCBIZUGJULUIUFUOUGACBS
      TBDCFHUAUBUCUDUE $.
      $([16-Oct-96]$)
  $}


