$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The Axiom of Regularity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A z $.
    zfregcl.1 $e |- A e. V $.
    $( The Axiom of Regularity with class variables. $)
    zfregcl $p |- ( E. x x e. A -> E. x e. A A. y e. x -. y e. A ) $=
      ( vz wel wex wn cv wral wrex wi wcel wceq eleq2 biexdv binegd biraldv
      rexeqd bi2imd hbre1 wal wa axreg df-ral birex df-rex bitr2 sylib 19.23ai
      vtocl ) AEFZAGZBEFZHZBAIZJZAEIZKZLUPCMZAGZBIZCMZHZBUPJZACKZLECDURCNZUMVAU
      SVFVGULUTAURCUPOPUQVEAURCVGUOVDBUPVGUNVCURCVBOQRSTULUSAUQAURUAULULBAFUOLB
      UBZUCAGZUSAEBUDUSVHAURKVIUQVHAURUOBUPUEUFVHAURUGUHUIUJUK $.
      $( [7-May-04] $) $( [5-Aug-94] $)
  $}

  ${
    $d x A y $.
    zfreg.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  Axiom 6 of [TakeutiZaring]
       p. 21.  This is called the 'weak form'; there is also a
       'strong form', not requiring that ` A ` be a set, that can be proved
       with more difficulty (see ~ zfregs ).  $)
    zfreg $p |- ( -. A = (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wceq cin zfregcl n0 disj birex 3imtr4 )
      AEZBFAGDEBFHDSIZABJBKLHSBMKLZABJADBCNABOUATABDSBPQR $.
      $( [7-May-04] $) $( [26-Nov-95] $)
  $}

  ${
    $d x A y $.  $( ` y ` is dummy. $)
    zfreg2.1 $e |- A e. V $.
    $( The Axiom of Regularity using abbreviations.  This form with the
       intersection arguments commuted (compared to ~ zfreg ) is formally
       more convenient for us in some cases.  Axiom Reg of [BellMachover]
       p. 480. $)
    zfreg2 $p |- ( -. A = (/) -> E. x e. A ( A i^i x ) = (/) ) $=
      ( vy cv wcel wex wn wral wrex c0 wceq cin zfregcl n0 incom cleq1i disj
      bitr birex 3imtr4 ) AEZBFAGDEBFHDUBIZABJBKLHBUBMZKLZABJADBCNABOUEUCABUEUB
      BMZKLUCUDUFKBUBPQDUBBRSTUA $.
      $( [7-May-04] $) $( [17-Sep-03] $)
  $}

  ${
    $d x y z $.  $( ` y z ` are dummy. $)
    $( The membership relation is irreflexive:  no set is a member of itself.
       Theorem 105 of [Suppes] p. 54.  (This is trivial to prove from ~ zfregfr
       and ~ efrirr , but this proof is direct from the Axiom of
       Regularity.) $)
    eirrv $p |- -. x e. x $=
      ( vz vy wel cv csn wcel wn wral wrex wex eleq1 visset snid a4w1 snex
      zfregcl ax-mp weq wi ax-14 eqcoms com12 elsn syl5ib binegd rcla4v mt2i
      nsyli con2d r19.21aiv ralnex sylib mt2 ) AADZBEZAEZFZGZHZBCEZIZCURJZVAURG
      ZCKVCVDUQURGZCAVAUQURLUQAMNZOCBURUQPQRUOVBHZCURIVCHUOVGCURUOVBVDUOVDACDZV
      BUOCASZVHVDVIUOVHUOVHTACACAUAUBUCCUQUDUEVBVHVEVFUTVEHBUQVABASUSVEUPUQURLU
      FUGUHUIUJUKVBCURULUMUN $.
      $( [19-Aug-93] $)
  $}

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( No class is a member of itself.  Exercise 6 of [TakeutiZaring] p. 22. $)
    eirr $p |- -. A e. A $=
      ( vx wcel wn wi wel cv wceq eleq2 eleq1 bitrd binegd eirrv vtoclg pm2.01
      ax-mp ) AACZQDZERBBFZDRBAABGZAHZSQUASTACQTATITAAJKLBMNQOP $.
      $( [7-Aug-94] $)

    $( A class is equal to its successor iff it is a proper class (assuming the
       Axiom of Regularity). $)
     sucprcreg $p |- ( -. A e. V <-> suc A = A ) $=
       ( vx cvv wcel wn csuc wceq sucprc cv wi wal eirr ax-17 eleq1 ceqsalg
       mtbiri csn wo cun wss ssid wb df-suc cleq1i sseq1 sylbi mpbiri sseld
       elun syl5ibr olc syl5 elsn 19.21aiv nsyl3 impbi ) ACDZEAFZAGZAHUQBIZAGZU
       TADZJZBKZUSUQVDAADZALVBVEBACVEBMUTAANOPUSVCBUSUTAQZDZVBVAUSVBVGRZVBVGUSU
       TAVFSZDVBVHUSVIAUTUSVIATZAATZAUAUSVIAGVJVKUBURVIAAUCUDVIAAUEUFUGUHUTAVFU
       IUJVGVBUKULBAUMUJUNUOUP $.
       $( [6-Jan-04] $) $( [9-Jul-04] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( The epsilon relation is founded on any class. $)
    zfregfr $p |- E Fr A $=
      ( vx vy cep wfr cv wss c0 wceq wn wa cin wrex wi dfepfr visset zfreg2
      adantl mpgbir ) ADEBFZAGZTHIJZKTCFLHICTMZNBBCAOUBUCUACTBPQRS $.
      $( [26-Nov-95] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $( ` x y ` are dummy. $)
    $( No class has 2-cycle membership loops.  Theorem 7X(b) of [Enderton]
       p. 206. $)
    en2lp $p |- -. ( A e. B /\ B e. A ) $=
      ( vx vy cvv wcel wa wn wel cv wceq eleq1 eleq2 bi2and binegd cep wfr
      zfregfr visset pm3.2i efrn2lp mp2an vtocl2g elisset im2an con3i pm2.61i
      ) AEFZBEFZGZABFZBAFZGZHZCDIZDCIZGZHZADJZFZUSAFZGZHUNCDABEECJZAKZUQVBVDUOU
      TUPVAVCAUSLVCAUSMNOUSBKZVBUMVEUTUKVAULUSBAMUSBALNOEPQVCEFZUSEFZGURERVFVGC
      SDSTCDEUAUBUCUMUJUKUHULUIABUDBAUDUEUFUG $.
      $( [16-Oct-96] $)

  $}

  ${
    preleq.1 $e |- A e. V $.
    preleq.2 $e |- B e. V $.
    preleq.3 $e |- C e. V $.
    preleq.4 $e |- D e. V $.
    $( Equality of two unordered pairs when one member of each pair contains
       the other member. $)
    preleq $p |- ( ( ( A e. B /\ C e. D ) /\ { A , B } = { C , D } ) ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wcel wa wn wo preq12b biimp ord en2lp eleq12 birand mtbiri
      syl6 a3d imp ancoms ) ABICDIJZABKZCDKZLZACJBDJLZUFUIUJUFUJUIUFUJMADJBCJLZ
      UIMUFUJUKUFUJUKNABCDEFGHOPQUKUIDCKZUHLDCRUKUGULUHADBCSTUAUBUCUDUE $.
      $( [16-Oct-96] $)

    $( Theorem for alternate representation of ordered pairs, requiring
       Regularity.  Exercise 34 of [Enderton] p. 207. $)
    opthreg $p |- ( { A , { A , B } } = { C , { C , D } } ->
                   ( A = C /\ B = D ) ) $=
      ( cpr wceq wa wcel pri1 pm3.2i prex preleq mpan preq1 cleq1d prer2
      syl6bi imdistani syl ) AABIZICCDIZIJZACJZUDUEJZKZUGBDJZKAUDLZCUELZKUFUIUK
      ULABEMCDGMNAUDCUEEABOGCDOPQUGUHUJUGUHCBIZUEJUJUGUDUMUEACBRSBDCFHTUAUBUC
      $.
      $( [16-Oct-96] $)
  $}

  $( The successor operation behaves like a one-to-one function (assuming the
     Axiom of Regularity).  Exercise 35 of [Enderton] p. 208 and its
     converse.  $)
  suc11reg $p |- ( suc A = suc B <-> A = B ) $=
    ( csuc wceq cvv wcel wi wa wn wo en2lp ianor mpbi eleq2 sucidg syl5bi
    com12 elsucg sylibd imp ord exp com23 syl5bir cleqcom syl6ib jaao mpi
    clneq sucexb bineg syl2anb pm2.21d ancoms syl5ib sucprc cleqan12d biimpd
    4cases suceq impbi ) ACZBCZDZABDZAEFZBEFZVDVEGZVFVGHABFZIZBAFZIZJZVHVIVKHIV
    MABKVIVKLMVFVJVHVGVLVFVDVJVEVFVDVJVEGVFVDHVIVEVFVDVIVEJZVFVDAVCFZVNVDVFVOVD
    AVBFVOVFVBVCANAEOPQABERSTUAUBUCVGVDVLVEVGVDVLVEGVGVDHZVLBADZVEVPVKVQVGVDVKV
    QJZVGVDBVBFZVRVDVGVSVDVSBVCFVGVBVCBNBEOUDQBAERSTUABAUEUFUBUCUGUHVFVGIZHVDVE
    VBEFZVCEFZIVDIVFVTVBVCEUIAUJZVGWBBUJZUKULUMVFIZVGHZVCVBDZVEVDWFWGVEVGWEWGIZ
    WBWAIWHVGWEVCVBEUIWDVFWAWCUKULUNUMVBVCUEUOWEVTHVDVEWEVTVBAVCBAUPBUPUQURUSAB
    UTVA $.
    $( [27-Oct-03] $) $( [25-Oct-03] $)

