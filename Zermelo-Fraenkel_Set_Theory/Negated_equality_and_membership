$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Negated equality and membership
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new connectives. $)
  $c =/= $. $( Not equal to (equal sign with slash through it). $)
  $c e/ $. $( Not an element of (epsilon with slash through it). $)

  $( Extend wff notation to include inequality. $)
  wne $a wff A =/= B $.

  $( Extend wff notation to include negated membership. $)
  wnel $a wff A e/ B $.

  $( Define inequality. $)
  df-ne $a |- ( A =/= B <-> -. A = B ) $.

  $( Define negated membership. $)
  df-nel $a |- ( A e/ B <-> -. A e. B ) $.

  $( Equality theorem for inequality. $)
  neeq1 $p |- ( A = B -> ( A =/= C <-> B =/= C ) ) $=
    ( wceq wn wne cleq1 binegd df-ne 3bitr4g ) ABDZACDZEBCDZEACFBCFKLMABCGHACIB
    CIJ $.
    $([19-Nov-94]$)

  $( Equality theorem for inequality. $)
  neeq2 $p |- ( A = B -> ( C =/= A <-> C =/= B ) ) $=
    ( wceq wn wne cleq2 binegd df-ne 3bitr4g ) ABDZCADZECBDZECAFCBFKLMABCGHCAIC
    BIJ $.
    $([19-Nov-94]$)

  ${
    neeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction for inequality. $)
    neeq1d   $p |- ( ph -> ( A =/= C <-> B =/= C ) ) $=
      ( wceq wne wb neeq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $([25-Oct-99]$)

    $( Deduction for inequality. $)
    neeq2d   $p |- ( ph -> ( C =/= A <-> C =/= B ) ) $=
      ( wceq wne wb neeq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $([25-Oct-99]$)
  $}


  $( Commutation of inequality. $)
  necom $p |- ( A =/= B <-> B =/= A ) $=
    ( wceq wn wne cleqcom bineg df-ne 3bitr4 ) ABCZDBACZDABEBAEJKABFGABHBAHI $.
    $([14-May-99]$)

  $( Equality theorem for negated membership. $)
  neleq1 $p |- ( A = B -> ( A e/ C <-> B e/ C ) ) $=
    ( wceq wcel wn wnel eleq1 binegd df-nel 3bitr4g ) ABDZACEZFBCEZFACGBCGLMN
    ABCHIACJBCJK $.
    $([20-Nov-94]$)

  $( Equality theorem for negated membership. $)
  neleq2 $p |- ( A = B -> ( C e/ A <-> C e/ B ) ) $=
    ( wceq wcel wn wnel eleq2 binegd df-nel 3bitr4g ) ABDZCAEZFCBEZFCAGCBGLMN
    ABCHICAJCBJK $.
    $([20-Nov-94]$)

