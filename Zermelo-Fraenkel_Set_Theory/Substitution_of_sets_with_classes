$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Substitution of sets with classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend wff notation to include substitution of sets with classes.
     Note that previously defined variable substitution is a special case. $)
  wsbc $a wff ( ph x | A ) $.

  $( Define the substitution of a set with a class.  This definition applies
     proper classes but is not meaningful in that case (and does not
     produce the same results as Quine's definition).  For an alternate
     definition see ~ dfsbcq . $)
  df-sbc $a |- ( ( ph x | A ) <-> A e. { x | ph } ) $.

  $( This serves as a weak definition that will allow us to use alternate
     definitions in place of ~ df-sbc above.  All results will be derived from
     starting from here instead of ~ df-sbc ; in particular, substitution will
     become undefined when ` A ` or ` B ` is a proper class.  Similar to Theorem
     6.7 of Quine, p. 42. $)
  dfsbcq $p |- ( A = B -> ( ( ph x | A ) <-> ( ph x | B ) ) ) $=
    ( wceq cab wcel wsbc eleq1 df-sbc 3bitr4g ) CDECABFZGDLGABCHABDHCDLIABCJABD
    JK $.
    $([14-Apr-95]$)

  ${
    $d ph y $.  $d A y $.  $d x y $.  $( ` y ` is dummy. $)
    $( Specialization:  if a formula is true for all sets, it is true for
       a class which is a set.  Similar to Theorem 6.11 of Quine, p. 44. $)
    a4sbc $p |- ( A e. V -> ( A. x ph -> ( ph x | A ) ) ) $=
      ( vy wal wsbc wi cv wceq wsb dfsbcq stdpc4 syl5bi vtocle ) ABEZABCFZGDCDH
      ZCIABDJPOABQCKABDLMN $.
      $([18-Aug-93]$)
  $}

  ${
    $d x y z $.  $d y A z $.  $d ph z $.  $( ` z ` is dummy. $)
    hbsbcl.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( If ` x ` is not free in ` A `, it is not free in ` ( ph x | A ) `. $)
    hbsbcl   $p |- ( A e. V -> ( ( ph x | A ) -> A. x ( ph x | A ) ) ) $=
      ( vz wsbc wal wi cv wceq wsb dfsbcq wel ax-17 hbeq biald hbsbv syl5bi
      sylbird vtocle ) ABDGZUBBHZIFDFJZDKZUBABFLZUCABUDDMZUEUFBHUCUFUEUFUBBBCCU
      DDCFNBOEPUGQABFRSTUA $.
      $([23-Aug-93]$)
  $}

  ${
    $d x A y $.  $( ` y ` is dummy. $)
    hbsbclv.1 $e |- A e. V $.
    $( If ` x ` does not occur in ` A `, it is not free in
       ` ( ph x | A ) `. $)
    hbsbclv $p |- ( ( ph x | A ) -> A. x ( ph x | A ) ) $=
      ( vy cvv wcel wsbc wal wi cv ax-17 hbsbcl ax-mp ) CFGABCHZOBIJDABECEKCGBL
      MN $.
      $([18-Aug-93]$)
  $}

  ${
    $d x A $.
    sbc5.1 $e |- A e. V $.
    $( An equivalence for class substitution. $)
    sbc5   $p |- ( ( ph x | A ) <-> E. x ( x = A /\ ph ) ) $=
      ( cv wceq wa wex wsbc hbsbclv wsb dfsbcq sbid syl5bb ceqsex bicom ) BEZCF
      ZAGBHABCIZASBCABCDJDRABBKSAABQCLABMNOP $.
      $([23-Aug-93]$)
  $}

  ${
    $d x A $.
    sbc6.1 $e |- A e. V $.
    $( An equivalence for class substitution. $)
    sbc6   $p |- ( ( ph x | A ) <-> A. x ( x = A -> ph ) ) $=
      ( cv wceq wi wal wsbc hbsbclv wsb dfsbcq sbid syl5bb ceqsal bicom ) BEZCF
      ZAGBHABCIZASBCABCDJDRABBKSAABQCLABMNOP $.
      $([23-Aug-93]$)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    sbc7.1 $e |- A e. V $.
    $( An equivalence for class substitution. $)
    sbc7   $p |- ( ( ph x | A ) <-> A e. { x | ph } ) $=
      ( df-sbc ) ABCE $.
      $([31-Dec-93]$)
  $}


