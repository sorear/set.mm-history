$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Power classes and the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for power class. $)
  $c P~ $.  $( Calligraphic P $)

  $( Extend class notation to include power class. $)
  cpw $a class P~ A $.

  ${
    $d x A $.
    $( Define power class.  Definition 5.10 of Takeuti and Zaring, p. 17,
       but we also let it apply to proper classes, i.e. those that are not
       members of ` V `. $)
    df-pw $a |- P~ A = { x | x (_ A } $.
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Equality theorem for the power class. $)
    pweq $p |- ( A = B -> P~ A = P~ B ) $=
      ( vx wceq cv wss cab cpw sseq2 biabdv df-pw 3eqtr4g ) ABDZCEZAFZCGNBFZCGA
      HBHMOPCABNIJCAKCBKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    ${
      sselpw.1 $e |- A e. V $.
      $( A subset of a class is a member of its power class.  Theorem 86,
          p. 47, of Suppes. $)
      sselpw   $p |- ( A (_ B <-> A e. P~ B ) $=
        ( vx cv wss cpw wcel sseq1 eleq1 df-pw cleqabi bicom vtoclbbv ) DEZBFZOBG
        ZHZABFAQHDACOABIOAQJRPPDQDBKLMN $.
        $([31-Dec-93]$)
    $}

    $( A subset of a class is a member of its power class.  Theorem 86, p. 47,
       of Suppes. $)
    sselpwg $p |- ( A e. C -> ( A (_ B <-> A e. P~ B ) ) $=
      ( vx cv wss cpw wcel wb wceq sseq1 eleq1 bi2bid visset sselpw vtoclg ) DEZB
      FZQBGZHZIABFZASHZIDACQAJRUATUBQABKQASLMQBDNOP $.
      $([6-Aug-00]$)
  $}

  $( A subset of a set is a member of its power set.  Theorem 86, p. 47, of
     Suppes. $)
  sselpw2g $p |- ( B e. C -> ( A (_ B <-> A e. P~ B ) ) $=
    ( wcel wss cpw wb wi cvv ssexg sselpwg syl6 ibib sylibr biimprd pm2.43i
    a1i impbid ) BCDZABEZABFZDZSTTUBGZHTUBHSTAIDUCABCJABIKLTUBMNUBTHSUBTUBTUBAB
    UAKOPQR $.
    $([7-Aug-00]$)

  ${
    pwid.1 $e |- A e. V $.
    $( A set is a member of its power set.  Theorem 87, p. 47, of Suppes. $)
    pwid   $p |- A e. P~ A $=
      ( wss cpw wcel ssid sselpw mpbi ) AACAADEAFAABGH $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x y z $.  $( ` x y z ` are dummy. $)
    zfpowcl.1 $e |- A e. V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of Takeuti and
       Zaring, p. 17. $)
    zfpowcl   $p |- P~ A e. V $=
      ( vz vx vy cv cpw cvv wcel wceq pweq eleq1d wex wel wss wb wal wi ax-pow
      df-ss birim bial biex mpbir bm1.3ii cab df-pw cleq2i cleqab bitr issetri
      vtoclbv ) CFZGZHIAGZHICABUMAJUNUOHUMAKLDUNDFZUNJZDMEDNZEFZUMOZPEQZDMUTDEU
      TURRZEQZDMDENDCNRDQZURRZEQZDMDECSVCVFDVBVEEUTVDURDUSUMTUAUBUCUDUEUQVADUQU
      PUTEUFZJVAUNVGUPEUMUGUHUTEUPUIUJUCUDUKUL $.
      $([5-Aug-93]$)
  $}

