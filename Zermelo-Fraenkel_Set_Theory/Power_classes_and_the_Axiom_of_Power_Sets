$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Power classes and the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for power class. $)
  $c P~ $.  $( Calligraphic P $)

  $( Extend class notation to include power class. $)
  cpw $a class P~ A $.

  ${
    $d x A $.
    $( Define power class.  Definition 5.10 of Takeuti and Zaring, p. 17,
       but we also let it apply to proper classes, i.e. those that are not
       members of ` V `. $)
    df-pw $a |- P~ A = { x | x (_ A } $.
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Equality theorem for the power class. $)
    pweq $p |- ( A = B -> P~ A = P~ B ) $=
      ( vx wceq cv wss cab cpw sseq2 biabdv df-pw 3eqtr4g ) ABDZCEZAFZCGNBFZCGA
      HBHMOPCABNIJCAKCBKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    sselpw.1 $e |- A e. V $.
    $( A subset of a class is a member of its power class.  Theorem 86, p. 47,
       of Suppes. $)
    sselpw   $p |- ( A (_ B <-> A e. P~ B ) $=
      ( vx cv wss cpw wcel sseq1 eleq1 df-pw cleqabi bicom vtoclbbv ) DEZBFZOBG
      ZHZABFAQHDACOABIOAQJRPPDQDBKLMN $.
      $([31-Dec-93]$)
  $}

  ${
    pwid.1 $e |- A e. V $.
    $( A set is a member of its power set.  Theorem 87, p. 47, of Suppes. $)
    pwid   $p |- A e. P~ A $=
      ( wss cpw wcel sselpw ssid mpbi ) AACAADEAABFAGH $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x y z $.  $( ` x y z ` are dummy. $)
    zfpowcl.1 $e |- A e. V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of Takeuti and
       Zaring, p. 17. $)
    zfpowcl   $p |- P~ A e. V $=
      ( vz vx vy cv cpw cvv wcel wceq pweq eleq1d wex wel wss wb wal cab df-pw
      cleq2i cleqab bitr biex wi df-ss birim bial ax-pow mpbir bm1.3ii issetri
      vtoclbv ) CFZGZHIAGZHICABUMAJUNUOHUMAKLDUNDFZUNJZDMEDNZEFZUMOZPEQZDMUQVAD
      UQUPUTERZJVAUNVBUPEUMSTUTEUPUAUBUCUTDEUTURUDZEQZDMDENDCNUDDQZURUDZEQZDMVD
      VGDVCVFEUTVEURDUSUMUEUFUGUCDECUHUIUJUIUKUL $.
      $([5-Aug-93]$)
  $}

