$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Power classes and the Axiom of Power Sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for power class. $)
  $c P~ $.  $( Calligraphic P $)

  $( Extend class notation to include power class. $)
  cpw $a class P~ A $.

  ${
    $d x A $.
    $( Define power class.  Definition 5.10 of [TakeutiZaring] p. 17,
       but we also let it apply to proper classes, i.e. those that are not
       members of ` V ` . $)
    df-pw $a |- P~ A = { x | x (_ A } $.
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Equality theorem for the power class. $)
    pweq $p |- ( A = B -> P~ A = P~ B ) $=
      ( vx wceq cv wss cab cpw sseq2 biabdv df-pw 3eqtr4g ) ABDZCEZAFZCGNBFZCGA
      HBHMOPCABNIJCAKCBKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x $.  $d B x $.  $( ` x ` is dummy. $)
    ${
      elpw.1 $e |- A e. V $.
      $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
      elpw   $p |- ( A e. P~ B  <-> A (_ B ) $=
        ( vx cv cpw wcel wss eleq1 sseq1 df-pw cleqabi vtoclbbv ) DEZBFZGNBHZAO
        GABHDACNAOINABJPDODBKLM $.
        $([9-Jan-04]$)
    $}

    $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
    elpwg $p |- ( A e. C -> ( A e. P~ B  <-> A (_ B ) ) $=
      ( vx cv cpw wcel wss wb wceq eleq1 sseq1 bi2bid visset elpw vtoclg ) DEZB
      FZGZQBHZIARGZABHZIDACQAJSUATUBQARKQABLMQBDNOP $.
      $([9-Jan-04]$)
  $}

  $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
  elpw2g $p |- ( B e. C -> ( A e. P~ B  <-> A (_ B ) ) $=
    ( wcel cpw wss wi elpwg ibi a1i id cvv wb ssexg syl6 pm5.74d mpbiri impbid
    ) BCDZABEZDZABFZUAUBGSUAUBABTHIJSUBUAGUBUBGUBKSUBUAUBSUBALDUAUBMABCNABLHOPQ
    R $.
    $([9-Jan-04]$)

  ${
    $d y z A $.  $d x y z $.
    hbpw.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for power class. $)
    hbpw $p |- ( y e. P~ A -> A. x y e. P~ A ) $=
      ( vz cv wss wal cpw wcel wel ax-17 hbel hbss visset elpw bial 3imtr4 ) BF
      ZCGZTAHSCIJZUAAHAESCEBKALABBEFCBEKALDMNSCBOPZUATAUBQR $.
      $([6-Nov-03]$)
  $}

  ${
    pwid.1 $e |- A e. V $.
    $( A set is a member of its power set.  Theorem 87 of [Suppes] p. 47. $)
    pwid   $p |- A e. P~ A $=
      ( cpw wcel wss ssid elpw mpbir ) AACDAAEAFAABGH $.
      $([5-Aug-93]$)
  $}

  ${
    $d A x y z $.  $( ` x y z ` are dummy. $)
    zfpowcl.1 $e |- A e. V $.
    $( Power set axiom expressed in class notation.  Axiom 4 of [TakeutiZaring]
       p. 17. $)
    zfpowcl   $p |- P~ A e. V $=
      ( vz vx vy cv cpw cvv wcel wceq pweq eleq1d wex wel wss wb wal wi axpow
      dfss2 birim bial biex mpbir bm1.3ii cab df-pw cleq2i cleqab bitr issetri
      vtoclbv ) CFZGZHIAGZHICABUMAJUNUOHUMAKLDUNDFZUNJZDMEDNZEFZUMOZPEQZDMUTDEU
      TURRZEQZDMDENDCNRDQZURRZEQZDMDECSVCVFDVBVEEUTVDURDUSUMTUAUBUCUDUEUQVADUQU
      PUTEUFZJVAUNVGUPEUMUGUHUTEUPUIUJUCUDUKUL $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.
    $( Power set axiom expressed in class notation, with the sethood
       requirement as an antecedent.  Axiom 4 of [TakeutiZaring] p. 17. $)
    zfpowclg $p |- ( A e. B -> P~ A e. V ) $=
      ( vx cv cpw cvv wcel wceq pweq eleq1d visset zfpowcl vtoclg ) CDZEZFGAEZF
      GCABNAHOPFNAIJNCKLM $.
      $([30-Oct-03]$)
  $}

