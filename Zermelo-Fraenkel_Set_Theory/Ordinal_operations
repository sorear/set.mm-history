$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordinal operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c +o $.  $( Ordinal addition operation (plus subscript o) $)
  $c .o $.  $( Ordinal multiplication operation (center dot subscript o) $)
  $c ^o $.  $( Ordinal exponentiation operation (caret subscript o) $)
  $c 1o $. $( The ordinal number 1 $)
  $c 2o $. $( The ordinal number 2 $)

  $( Extend the definition of a class to include the ordinal addition
     operation. $)
  coa $a class +o $.

  $( Extend the definition of a class to include the ordinal multiplication
     operation. $)
  comu $a class .o $.

  $( Extend the definition of a class to include the ordinal exponentiation
     operation. $)
  coe $a class ^o $.

  $( Extend the definition of a class to include the ordinal number 1. $)
  c1o $a class 1o $.

  $( Extend the definition of a class to include the ordinal number 2. $)
  c2o $a class 2o $.

  ${
    $d x y z w v $.
    $( Define ordinal addition operation. $)
    df-oadd $a |- +o = { <. <. x , y >. , z >. | ( ( x e. On /\ y e. On ) /\
                z = ( rec ( { <. w , v >. | v = suc w } , x ) ` y ) ) } $.
  $}

  ${
    $d x y z w v $.
    $( Define ordinal multiplication operation. $)
    df-omul $a |- .o = { <. <. x , y >. , z >. | ( ( x e. On /\ y e. On ) /\
             z = ( rec ( { <. w , v >. | v = ( w +o x ) } , (/) ) ` y ) ) } $.
  $}

  ${
    $d x y z w v $.
    $( Define ordinal exponentiation operation. $)
    df-oexp $a |- ^o = { <. <. x , y >. , z >. | ( ( x e. On /\ y e. On ) /\
          z = ( ( rec ( { <. w , v >. | v = ( w .o x ) } , 1o ) ` y )
              i^i ( ( V \ |^| x ) u. |^| y ) ) ) } $.
    $(
    df-oexp2 $a |- ^o = { <. <. x , y >. , z >. | ( ( x e. On /\ y e. On ) /\
               ( -. x = (/) /\ z =
                 ( rec ( { <. w , v >. | v = ( w .o x ) } , 1o ) ` y ) )
               \/ ( x = (/) /\ z = ( 1o \ y ) ) ) ) } $.
    $)
  $}

  $( Define the ordinal number 1. $)
  df-1o $a |- 1o = suc (/) $.

  $( Define the ordinal number 2. $)
  df-2o $a |- 2o = suc 1o $.

  $( Expanded value of the ordinal number 1. $)
  df1o2 $p |- 1o = { (/) } $=
    ( c1o c0 csuc csn cun df-1o df-suc uncom un0 eqtr 3eqtr ) ABCBBDZEZLFBGMLBE
    LBLHLIJK $.
    $( [4-Nov-02] $)

  $( Expanded value of the ordinal number 2. $)
  df2o2 $p |- 2o = { (/) , { (/) } } $=
    ( c2o c1o csuc csn cun c0 cpr df-2o df-suc df1o2 sneqi uneq12i df-pr eqtr4
    3eqtr ) ABCBBDZEZFFDZGZHBIQRRDZESBRPTJBRJKLFRMNO $.
    $( [19-Feb-04] $) $( [29-Jan-04] $)

  $( Ordinal zero is not equal to ordinal one. $)
  0ne1o $p |- -. (/) = 1o $=
    ( c0 c1o wceq csn 0nep0 df1o2 cleq2i mtbir ) ABCAADZCEBIAFGH $.
    $( [?] $) $( [3-Sep-04] $)

  ${
    $d x y z w v $.  $( ` x y z w v ` are dummy. $)
    $( Functionality and domain of ordinal addition. $)
    fnoa $p |- +o Fn ( On X. On ) $=
      ( vx vy vz vv vw con0 cv csuc wceq copab crdg cfv coa fvex df-oadd
      fnoprab2 ) ABCFFBGZDGEGHIEDJAGKZLMQRNABCEDOP $.
      $( [26-Aug-95] $)
  $}

  ${
    $d x y z w v $.  $( ` x y z w v ` are dummy. $)
    $( Functionality and domain of ordinal multiplication. $)
    fnom $p |- .o Fn ( On X. On ) $=
      ( vx vy vz vv vw con0 cv coa co wceq copab c0 crdg cfv comu fvex df-omul
      fnoprab2 ) ABCFFBGZDGEGAGHIJEDKLMZNOSTPABCEDQR $.
      $( [26-Aug-95] $)
  $}

  ${
    $d x y z w v $.  $d z w v A $.  $d z w v B $.  $( ` z w v ` are dummy. $)
    $( Value of ordinal addition. $)
    oav $p |- ( ( A e. On /\ B e. On ) ->
             ( A +o B ) = ( rec ( { <. x , y >. | y = suc x } , A ) ` B ) ) $=
      ( vw vv vz con0 cv csuc wceq copab crdg cfv coa fvex rdgeq2 fveq1d fveq2
      df-oadd oprabval2 ) EFGCDHHFIZBIAIJKABLZEIZMZNDUCCMZNOUBUFNDUFPUDCKUBUEUF
      UDCUCQRUBDUFSEFGABTUA $.
      $( [3-May-95] $)
  $}

  ${
    $d x y z w v A $.  $d z w v B $.  $( ` z w v ` are dummy. $)
    $( Value of ordinal multiplication. $)
    omv $p |- ( ( A e. On /\ B e. On ) ->
     ( A .o B ) = ( rec ( { <. x , y >. | y = ( x +o A ) } , (/) ) ` B ) ) $=
      ( vw vv vz con0 cv coa co wceq copab c0 crdg cfv comu fvex opreq2 cleq2d
      biopabdv rdgeq1 fveq1 3syl fveq2 df-omul oprabval2 ) EFGCDHHFIZBIZAIZEIZJ
      KZLZABMZNOZPZDUIUJCJKZLZABMZNOZPQUHUTPZDUTRUKCLZUNUSLUOUTLUPVALVBUMURABVB
      ULUQUIUKCUJJSTUANUNUSUBUHUOUTUCUDUHDUTUEEFGABUFUG $.
      $( [17-Sep-95] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Addition with zero.  Proposition 8.3 of [TakeutiZaring] p. 57. $)
    oa0 $p |- ( A e. On -> ( A +o (/) ) = A ) $=
      ( vy vx con0 wcel c0 coa co cv csuc wceq copab crdg cfv 0elon oav mpan2
      rdgzert eqtrd ) ADEZAFGHZFBICIJKCBLZAMNZATFDEUAUCKOCBAFPQADUBRS $.
      $( [3-May-95] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( Ordinal multiplication with zero.  Definition 8.15 of [TakeutiZaring]
       p. 62. $)
    om0 $p |- ( A e. On -> ( A .o (/) ) = (/) ) $=
      ( vy vx con0 wcel c0 comu co cv coa wceq copab crdg cfv 0elon omv mpan2
      0ex rdgzer syl6eq ) ADEZAFGHZFBICIAJHKCBLZFMNZFUAFDEUBUDKOCBAFPQFUCRST $.
      $( [17-Sep-95] $)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( Ordinal multiplication with zero.  Definition 8.15 of [TakeutiZaring]
       p. 62.  Unlike ~ om0 , this version works whether or not ` A ` is
       an ordinal.  However it since it is an artifact of our particular
       function value definition outside the domain, we will not use it
       in order to be conventional and present it only as a curiosity. $)
    om0x $p |- ( A .o (/) ) = (/) $=
      ( vy vx con0 wcel c0 wa comu co wceq cv coa copab crdg cfv omv 0ex
      rdgzer syl6eq cxp wfn cdm fnom fndm ax-mp ndmopr pm2.61i ) ADEFDEGZAFHIZF
      JUHUIFBKCKALIJCBMZFNOFCBAFPFUJQRSAFDHQHDDTZUAHUBUKJUCUKHUDUEUFUG $.
      $( [1-Feb-96] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Addition with successor.  Definition 8.1 of [TakeutiZaring] p. 56. $)
    oasuc $p |- ( ( A e. On /\ B e. On ) ->
                ( A +o suc B ) = suc ( A +o B ) ) $=
      ( vy vx con0 wcel wa csuc cv wceq copab crdg cfv coa co rdgsuct adantl
      oav suceloni sylan5 fveq2d oprex sucex suceq fvopab syl5eqr 3eqtr4d ) AEF
      ZBEFZGZBHZCIDIZHZJDCKZALZMZBUOMZUNMZAUKNOZABNOZHZUIUPURJUHABUNPQUHUKEFUSU
      PJUIDCAUKRBSTUJUTUNMURVAUJUTUQUNDCABRUADCUTUMVAABNUBZUTVBUCULUTUDUEUFUG
      $.
      $( [3-May-95] $)
  $}

  $( Addition with 1 is same as successor. $)
  oa1suc $p |- ( A e. On ->
                ( A +o 1o ) = suc A ) $=
    ( con0 wcel c1o coa co c0 csuc wceq 0elon oasuc mpan2 df-1o opreq2i syl5eq
    oa0 suceq syl eqtrd ) ABCZADEFZAGEFZHZAHZTAGHZEFZUCUATGBCUFUCIJAGKLDUEAEMNO
    TUBAIUCUDIAPUBAQRS $.
    $( [29-Oct-95] $)

  ${
    $d x y A $.  $d x y B $.
    $( Multiplication with successor.  Definition 8.15 of [TakeutiZaring]
       p. 62. $)
    omsuc $p |- ( ( A e. On /\ B e. On ) ->
                ( A .o suc B ) = ( ( A .o B ) +o A ) ) $=
      ( vy vx con0 wcel wa csuc cv coa co wceq copab c0 crdg cfv comu rdgsuct
      adantl omv suceloni sylan5 fveq2d oprex opreq1 fvopab syl5eqr 3eqtr4d )
      AEFZBEFZGZBHZCIDIZAJKZLDCMZNOZPZBUPPZUOPZAULQKZABQKZAJKZUJUQUSLUINBUORSUI
      ULEFUTUQLUJDCAULTBUAUBUKVAUOPUSVBUKVAURUODCABTUCDCVAUNVBABQUDVAAJUDUMVAAJ
      UEUFUGUH $.
      $( [17-Sep-95] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x y z $.
    $( Ordinal addition with a limit ordinal.  Definition 8.1 of
       [TakeutiZaring] p. 56. $)
    oalim $p |- ( ( A e. On /\ ( B e. C /\ Lim B ) ) ->
                ( A +o B ) = U. x e. B ( A +o x ) ) $=
      ( vz vy con0 wcel wlim wa coa co cv ciun wceq csuc copab crdg cfv
      rdglim2a adantl wb oav onelon sylan5 anassrs exp iuneq2dv cleq12d
      adantrr mpbird limelon pm3.27 jca ) BGHZCGHZCIZJZBCKLZACBAMZKLZNZOZCDHZUQ
      JZUOURJVCCEMFMPOFEQZBRZSZACUTVGSZNZOZURVKUOABCGVFTUAUOUPVCVKUBUQUOUPJZUSV
      HVBVJFEBCUCVLACVAVIVLUTCHZVAVIOZUOUPVMVNUOUTGHVNUPVMJFEBUTUCCUTUDUEUFUGUH
      UIUJUKVEUPUQCDULVDUQUMUNUE $.
      $( [5-Aug-04] $) $( [3-Aug-04] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    $( Ordinal multiplication with a limit ordinal.  Definition 8.1 of
       [TakeutiZaring] p. 56. $)
    omlim $p |- ( ( A e. On /\ ( B e. C /\ Lim B ) ) ->
                ( A .o B ) = U. x e. B ( A .o x ) ) $=
      ( vz vy con0 wcel wlim wa comu co cv ciun wceq coa copab c0 crdg cfv
      rdglim2a adantl wb omv onelon sylan5 anassrs exp iuneq2dv cleq12d
      adantrr mpbird limelon pm3.27 jca ) BGHZCGHZCIZJZBCKLZACBAMZKLZNZOZCDHZUR
      JZUPUSJVDCEMFMBPLOFEQZRSZTZACVAVHTZNZOZUSVLUPARCGVGUAUBUPUQVDVLUCURUPUQJZ
      UTVIVCVKFEBCUDVMACVBVJVMVACHZVBVJOZUPUQVNVOUPVAGHVOUQVNJFEBVAUDCVAUEUFUGU
      HUIUJUKULVFUQURCDUMVEURUNUOUF $.
      $( [5-Aug-04] $) $( [3-Aug-04] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure law for ordinal addition.  Proposition 8.2 of [TakeutiZaring]
       p. 57. $)
    oacl $p |- ( ( A e. On /\ B e. On ) -> ( A +o B ) e. On ) $=
      ( vx vy con0 wcel coa co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq oa0
      ibir wa oasuc suceloni syl5bir exp com12 a2d wlim wral ciun cvv visset
      oalim mpan21 pm2.27 r19.20sdv imp oprex iunon syl exp4b com4r pm2.43i
      com3l tfinds ) AEFZBEFZABGHZEFZVFVEVHVEACIZGHZEFZJVEAKGHZEFZJVEADIZGHZEFZ
      JZVEAVNLZGHZEFZJVEVHJCDBVIKMZVKVMVEWAVJVLEVIKAGNOPCDQZVKVPVEWBVJVOEVIVNAG
      NOPVIVRMZVKVTVEWCVJVSEVIVRAGNOPVIBMZVKVHVEWDVJVGEVIBAGNOPVEVMVEVLAEAROSVN
      EFZVEVPVTVEWEVPVTJZVEWEWFVEWETZVTVOLZEFVPWGVSWHEAVNUAOVOUBUCUDUEUFVEVIUGZ
      VQDVIUHZVKVEWIWJVKJJVEWIWJVEVKVEWIWJVEVKVEWITZVKDVIVOUIZEFZWJVETZWKVJWLEV
      EVIUJFWIVJWLMCUKZDAVIUJULUMOWNVPDVIUHZWMWJVEWPVEWJWPVEVQVPDVIVEVPUNUOUEUP
      DVIVOWOAVNGUQURUSUCUTVAVBVCVDUEUP $.
      $( [5-May-95] $)
  $}

  ${
    $d x y A $.  $d x B $.
    $( Closure law for ordinal multiplication.  Proposition 8.16 of
       [TakeutiZaring] p. 57. $)
    omcl $p |- ( ( A e. On /\ B e. On ) -> ( A .o B ) e. On ) $=
      ( vx vy con0 wcel comu co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq
      0elon om0 mpbiri wa coa omsuc oacl syl5bir exp4b com24 pm2.43i com3r a2d
      wlim wral ciun cvv visset omlim mpan21 pm2.27 r19.20sdv com12 imp oprex
      iunon syl com4r com3l tfinds ) AEFZBEFZABGHZEFZVIVHVKVHACIZGHZEFZJVHAKGHZ
      EFZJVHADIZGHZEFZJZVHAVQLZGHZEFZJVHVKJCDBVLKMZVNVPVHWDVMVOEVLKAGNOPCDQZVNV
      SVHWEVMVREVLVQAGNOPVLWAMZVNWCVHWFVMWBEVLWAAGNOPVLBMZVNVKVHWGVMVJEVLBAGNOP
      VHVPKEFRVHVOKEASOTVQEFZVHVSWCVHVSWHWCVHVSWHWCJJVHWHVSVHWCVHWHVSVHWCVHWHUA
      ZWCVRAUBHZEFVSVHUAWIWBWJEAVQUCOVRAUDUEUFUGUHUIUJVHVLUKZVTDVLULZVNVHWKWLVN
      JJVHWKWLVHVNVHWKWLVHVNVHWKUAZVNDVLVRUMZEFZWLVHUAZWMVMWNEVHVLUNFWKVMWNMCUO
      ZDAVLUNUPUQOWPVSDVLULZWOWLVHWRVHWLWRVHVTVSDVLVHVSURUSUTVADVLVRWQAVQGVBVCV
      DUEUFVEUHVFVGUTVA $.
      $( [6-Aug-04] $) $( [3-Aug-04] $)
  $}

  ${
    $d x y $.  $d x A $.
    $( Ordinal addition with zero.  Proposition 8.3 of [TakeutiZaring]
       p. 57. $)
    oa0r $p |- ( A e. On -> ( (/) +o A ) = A ) $=
      ( vx vy c0 cv coa co wceq csuc opreq2 id cleq12d weq con0 wcel 0elon oa0
      ax-mp oasuc mpan suceq sylan9eq exp wlim ciun cuni wral cvv visset wa
      oalim limuni iuneq2 uniiun syl6eqr syl5bir tfinds ) DBEZFGZURHZDDFGZDHZDC
      EZFGZVCHZDVCIZFGZVFHZDAFGZAHBCAURDHZUSVAURDURDDFJVJKLBCMZUSVDURVCURVCDFJV
      KKLURVFHZUSVGURVFURVFDFJVLKLURAHZUSVIURAURADFJVMKLDNOZVBPDQRVCNOZVEVHVOVE
      VGVDIZVFVNVOVGVPHPDVCSTVDVCUAUBUCURUDZUTCURVDUEZURUFZHVECURUGZVQUSVRURVSU
      RUHOZVQUSVRHZBUIVNWAVQUJWBPCDURUHUKTTURULLVTVRCURVCUEVSCURVDVCUMCURUNUOUP
      UQ $.
      $( [5-May-95] $)
  $}

  ${
    $d x y A $.
    $( Ordinal multiplication with zero.  Proposition 8.18(1) of
       [TakeutiZaring] p. 63. $)
    om0r $p |- ( A e. On -> ( (/) .o A ) = (/) ) $=
      ( vx vy c0 cv comu co wceq csuc opreq2 cleq1d weq om0x con0 wcel coa
      0elon omsuc mpan oa0 ax-mp cleqcomi a1i cleq12d opreq1 syl5bir wlim ciun
      wral cvv visset wa omlim iuneq2 iun0 syl6eq tfinds ) DBEZFGZDHZDDFGZDHDCE
      ZFGZDHZDVBIZFGZDHZDAFGZDHBCAURDHUSVADURDDFJKBCLUSVCDURVBDFJKURVEHUSVFDURV
      EDFJKURAHUSVHDURADFJKDMVBNOZVGVCDPGZDDPGZHVDVIVFVJDVKDNOZVIVFVJHQDVBRSDVK
      HVIVKDVLVKDHQDTUAUBUCUDVCDDPUEUFURUGZUTCURVCUHZDHVDCURUIZVMUSVNDURUJOZVMU
      SVNHZBUKVLVPVMULVQQCDURUJUMSSKVOVNCURDUHDCURVCDUNCURUOUPUFUQ $.
      $( [5-Aug-04] $) $( [3-Aug-04] $)
  $}

  $( Ordinal 1 is an ordinal number. $)
  1o $p |- 1o e. On $=
    ( c1o c0 csuc con0 df-1o 0elon onsuc eqeltr ) ABCDEBFGH $.
    $( [29-Oct-95] $)

  $( Ordinal 2 is an ordinal number. $)
  2o $p |- 2o e. On $=
    ( c1o con0 wcel c2o 1o csuc sucelon df-2o eleq1i bitr4 mpbi ) ABCZDBCZELAFZ
    BCMAGDNBHIJK $.
    $( [19-Feb-04] $) $( [18-Feb-04] $)

  $( 1 + 1 = 2 for ordinal numbers. $)
  o1p1e2 $p |- ( 1o +o 1o ) = 2o $=
    ( c1o coa co csuc c2o con0 wcel wceq 1o oa1suc ax-mp df-2o eqtr4 ) AABCZADZ
    EAFGNOHIAJKLM $.
    $( [19-Feb-04] $) $( [18-Feb-04] $)

  $( Ordinal multiplication with 1.  Proposition 8.18(2) of [TakeutiZaring]
     p. 63. $)
  om1 $p |- ( A e. On -> ( A .o 1o ) = A ) $=
    ( con0 wcel c1o comu co c0 coa csuc wceq 0elon omsuc mpan2 df-1o opreq2i
    syl5eq om0 opreq1d oa0r 3eqtrd ) ABCZADEFZAGEFZAHFZGAHFAUAAGIZEFZUDUBUAGBCU
    FUDJKAGLMDUEAENOPUAUCGAHAQRAST $.
    $( [29-Oct-95] $)

  ${
    $d x y $.  $d x A $.
    $( Ordinal multiplication with 1.  Proposition 8.18(2) of [TakeutiZaring]
       p. 63. $)
    om1r $p |- ( A e. On -> ( 1o .o A ) = A ) $=
      ( vx vy c1o cv comu co wceq c0 csuc opreq2 id cleq12d weq om0x con0 wcel
      wa coa 1o omsuc mpan opreq1 sylan9eq oa1suc adantr eqtrd exp wlim ciun
      cuni wral cvv visset omlim limuni iuneq2 uniiun syl6eqr syl5bir tfinds )
      DBEZFGZVBHZDIFGZIHDCEZFGZVFHZDVFJZFGZVIHZDAFGZAHBCAVBIHZVCVEVBIVBIDFKVMLM
      BCNZVCVGVBVFVBVFDFKVNLMVBVIHZVCVJVBVIVBVIDFKVOLMVBAHZVCVLVBAVBADFKVPLMDOV
      FPQZVHVKVQVHRVJVFDSGZVIVQVHVJVGDSGZVRDPQZVQVJVSHTDVFUAUBVGVFDSUCUDVQVRVIH
      VHVFUEUFUGUHVBUIZVDCVBVGUJZVBUKZHVHCVBULZWAVCWBVBWCVBUMQZWAVCWBHZBUNVTWEW
      ARWFTCDVBUMUOUBUBVBUPMWDWBCVBVFUJWCCVBVGVFUQCVBURUSUTVA $.
      $( [7-Aug-04] $) $( [3-Aug-04] $)
  $}


