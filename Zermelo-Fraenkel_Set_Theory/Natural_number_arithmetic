$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Addition with zero.  Theorem 4I(A1) of [Enderton] p. 79. $)
  nna0 $p |- ( A e. om -> ( A +o (/) ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnont oa0 syl ) ABCADCAEFGAHAIAJK $.
    $([20-Sep-95]$)

  $( Multiplication with zero.  Theorem 4J(A1) of [Enderton] p. 80. $)
  nnm0 $p |- ( A e. om -> ( A .o (/) ) = (/) )  $=
    ( com wcel con0 c0 comu co wceq nnont om0 syl ) ABCADCAEFGEHAIAJK $.
    $([20-Sep-95]$)

  $( Addition with successor.  Theorem 4I(A2) of [Enderton] p. 79. $)
  nnasuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A +o suc B ) = suc ( A +o B ) ) $=
    ( con0 wcel csuc coa co wceq com oasuc nnont syl2an ) ACDBCDABEFGABFGEHAIDB
    IDABJAKBKL $.
    $([20-Sep-95]$)

  $( Multiplication with successor.  Theorem 4J(A2) of [Enderton] p. 80. $)
  nnmsuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A .o suc B ) = ( ( A .o B ) +o A ) ) $=
    ( con0 wcel csuc comu co coa wceq com omsuc nnont syl2an ) ACDBCDABEFGABFGA
    HGIAJDBJDABKALBLM $.
    $([20-Sep-95]$)

  $( Addition to zero.  Remark in proof of Theorem 4K(2) of [Enderton] p. 81. $)
  nna0r $p |- ( A e. om -> ( (/) +o A ) = A ) $=
    ( com wcel con0 c0 coa co wceq nnont oa0r syl ) ABCADCEAFGAHAIAJK $.
    $([20-Sep-95]$)

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Multiplication with zero.  Exercise 16 of [Enderton] p. 82. $)
    nnm0r $p |- ( A e. om -> ( (/) .o A ) = (/) ) $=
      ( vx vy c0 cv comu co wceq csuc opreq2 cleq1d weq com wcel peano1 nnm0
      ax-mp coa nnmsuc mpan nna0 cleqcomi a1i cleq12d opreq1 syl5bir finds ) DB
      EZFGZDHDDFGZDHZDCEZFGZDHZDULIZFGZDHZDAFGZDHBCAUHDHUIUJDUHDDFJKBCLUIUMDUHU
      LDFJKUHUOHUIUPDUHUODFJKUHAHUIURDUHADFJKDMNZUKODPQULMNZUQUMDRGZDDRGZHUNUTU
      PVADVBUSUTUPVAHODULSTDVBHUTVBDUSVBDHODUAQUBUCUDUMDDRUEUFUG $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers.  Proposition 8.9 of [TakeutiZaring]
       p. 59. $)
    nnacl $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) e. om ) $=
      ( vx vy com wcel coa co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq nna0
      biimprd pm2.43i wa nnasuc peano2b biimp syl5bir exp comm a2d finds imp )
      AEFZBEFZABGHZEFZULUKUNUKACIZGHZEFZJUKAKGHZEFZJUKADIZGHZEFZJUKAUTLZGHZEFZJ
      UKUNJCDBUOKMZUQUSUKVFUPUREUOKAGNOPCDQZUQVBUKVGUPVAEUOUTAGNOPUOVCMZUQVEUKV
      HUPVDEUOVCAGNOPUOBMZUQUNUKVIUPUMEUOBAGNOPUKUSUKUSUKUKURAEAROSTUTEFZUKVBVE
      UKVJVBVEJZUKVJVKUKVJUAZVEVALZEFZVBVLVDVMEAUTUBOVBVNVAUCUDUEUFUGUHUIUGUJ
      $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmcl $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) e. om ) $=
      ( vx vy com wcel comu co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq
      peano1 nnm0 mpbiri wa coa nnmsuc nnacl syl5bir exp4b com24 pm2.43i com3r
      a2d finds comm imp ) AEFZBEFZABGHZEFZUOUNUQUNACIZGHZEFZJUNAKGHZEFZJUNADIZ
      GHZEFZJUNAVCLZGHZEFZJUNUQJCDBURKMZUTVBUNVIUSVAEURKAGNOPCDQZUTVEUNVJUSVDEU
      RVCAGNOPURVFMZUTVHUNVKUSVGEURVFAGNOPURBMZUTUQUNVLUSUPEURBAGNOPUNVBKEFRUNV
      AKEASOTVCEFZUNVEVHUNVEVMVHUNVEVMVHJJUNVMVEUNVHUNVMVEUNVHUNVMUAZVHVDAUBHZE
      FVEUNUAVNVGVOEAVCUCOVDAUDUEUFUGUHUIUJUKULUM $.
      $([20-Sep-95]$)
  $}

  ${
    $d x A $.  $d x y z B $.  $( ` x y z` are dummy. $)
    $( Addition of natural numbers is commutative.  Theorem 4K(2) of
       [Enderton] p. 81. $)
    nnacom $p |- ( ( A e. om /\ B e. om ) -> ( A +o B ) = ( B +o A ) ) $=
      ( vx vy vz com wcel coa co wceq cv wi c0 csuc opreq1 opreq2 cleq12d
      bilimd weq nna0r nna0 eqtr4d wa suceq syl peano2b sylbi con0 oasuc nnont
      suceloni syl2an im2an 3syl syl5bir exp comm a2d finds imp nnasuc ) AFGBFG
      ZABHIZBAHIZJZVBCKZBHIZBVFHIZJZLVBMBHIZBMHIZJZLVBDKZBHIZBVMHIZJZLVBVMNZBHI
      ZBVQHIZJZLVBVELCDAVFMJZVIVLVBWAVGVJVHVKVFMBHOVFMBHPQRCDSZVIVPVBWBVGVNVHVO
      VFVMBHOVFVMBHPQRVFVQJZVIVTVBWCVGVRVHVSVFVQBHOVFVQBHPQRVFAJZVIVEVBWDVGVCVH
      VDVFABHOVFABHPQRVBVJBVKBTBUAUBVMFGZVBVPVTVBWEVPVTLZVBWEWFVBWEUCZVTVNNZVON
      ZJVPWGVRWHVSWIVBWEVRWHJZWEVQVFHIZVMVFHIZNZJZLWEVQMHIZVMMHIZNZJZLWEVQEKZHI
      ZVMWSHIZNZJZLWEVQWSNZHIZVMXDHIZNZJZLWEWJLCEBWAWNWRWEWAWKWOWMWQVFMVQHPWAWL
      WPJWMWQJVFMVMHPWLWPUDUEQRCESZWNXCWEXIWKWTWMXBVFWSVQHPXIWLXAJWMXBJVFWSVMHP
      WLXAUDUEQRVFXDJZWNXHWEXJWKXEWMXGVFXDVQHPXJWLXFJWMXGJVFXDVMHPWLXFUDUEQRVFB
      JZWNWJWEXKWKVRWMWHVFBVQHPXKWLVNJWMWHJVFBVMHPWLVNUDUEQRWEWOVQWQWEVQFGWOVQJ
      VMUFVQUAUGWEWPVMJWQVQJVMUAWPVMUDUEUBWSFGZWEXCXHWEXLXCXHLZWEXLXMWEXLUCZXHW
      TNZXBNZJXCXNXEXOXGXPVQUHGZWSUHGZXEXOJWEXLVQWSUIWEVMUHGZXQVMUJZVMUKUEWSUJZ
      ULXNXSXRUCXFXBJXGXPJWEXSXLXRXTYAUMVMWSUIXFXBUDUNQWTXBUDUOUPUQURUSUTBVMVAQ
      VNVOUDUOUPUQURUSUT $.
      $([6-May-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Ordering property of addition.  Half of Proposition 8.4 of
       [TakeutiZaring] p. 58, limited to natural numbers. $)
    nnaordi $p |- ( ( B e. om /\ C e. om ) ->
                 ( A e. B -> ( C +o A ) e. ( C +o B ) ) ) $=
      ( vx vy com wcel coa co wi wa con0 w3a csuc wss cv wceq opreq2 sseq2d
      bilimd weq ssid a1i oasuc ancoms sssucid sstr mpi syl5bir nnont sylan
      exp ad2antll a2d findsg peano2b sylan5b word ordsucss imp nnord syl5
      exp4b pm2.43b com34 3imp sseq1d cvv oprex sucssel ax-mp syl6bi sylan5
      3adant2 syld syl3an3 elnn syl3an1 3exp com4r exp3a pm2.43i pm2.43d com3l
      ) BFGZCFGZABGZCAHIZCBHIZGZJZWGWEWFWJWGWEWFWJJZWGWEWEWLJZJWGWGWEWMWGWEKZWE
      WFWGWJWNWEWFWKAFGZWEWFWKWNWOWECLGZWKWFWOWEWPMWGCANZHIZWIOZWJWOWEWPWGWSJWO
      WEWGWPWSWOWEWGWPWSJZJWEWOWEWGWTWEWOKWQBOZWTWEWGKWEWQFGZXAWTJWOWEXBKXAWTWP
      WRCDPZHIZOZJWPWRWROZJZWPWRCEPZHIZOZJWPWRCXHNZHIZOZJWTDEBWQXCWQQZXEXFWPXNX
      DWRWRXCWQCHRSTDEUAZXEXJWPXOXDXIWRXCXHCHRSTXCXKQZXEXMWPXPXDXLWRXCXKCHRSTXC
      BQZXEWSWPXQXDWIWRXCBCHRSTXGXBXFWPWRUBUCUCXHFGZXBKWQXHOZKWPXJXMXRWPXJXMJZJ
      XBXSXRWPXTXHLGZWPXTXRYAWPKZXMWRXINZOZXJYBXLYCWRWPYAXLYCQCXHUDUESXJXIYCOYD
      XIUFWRXIYCUGUHUIXHUJUKULUMUNUOULAUPUQBURZWGXAWEYEWGXAABUSUTBVAUKVBVCVDVEV
      FWOWPWSWJJZWEWPWOYFWPALGZYFWOWPYGKZWSWHNZWIOZWJYHWRYIWICAUDVGWHVHGYJWJJCA
      HVIWHWIVHVJVKVLAUJVMUEVNVOCUJVPABVQVRVSVTWAWBWCWDUT $.
      $([3-Feb-96]$)
  $}

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring]
     p. 58, limited to natural numbers. $)
  nnaord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( C +o A ) e. ( C +o B ) ) ) $=
    ( com wcel w3a coa co wi nnaordi 3adant1 wceq wo wn opreq2 a1i 3adant2
    im2ord pm2.16d word wa wb df-3an ancom anandi 3bitr nnacl nnord syl im2an
    sylbi ordtri2 3simpa 3syl 3imtr4d impbid ) ADEZBDEZCDEZFZABEZCAGHZCBGHZEZUR
    USVAVDIUQABCJKUTVBVCLZVCVBEZMZNZABLZBAEZMZNZVDVAUTVKVGUTVIVEVJVFVIVEIUTABCG
    OPUQUSVJVFIURBACJQRSUTVBTZVCTZUAZVDVHUBUTUSUQUAZUSURUAZUAZVOUTUQURUAZUSUAUS
    VSUAVRUQURUSUCVSUSUDUSUQURUEUFVPVMVQVNVPVBDEVMCAUGVBUHUIVQVCDEVNCBUGVCUHUIU
    JUKVBVCULUIUTVSATZBTZUAVAVLUBUQURUSUMUQVTURWAAUHBUHUJABULUNUOUP $.
    $([7-Mar-96]$)

  $( Ordering property of addition.  Proposition 8.4 of [TakeutiZaring]
     p. 58, limited to natural numbers. $)
  nnaordr $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( A +o C ) e. ( B +o C ) ) ) $=
    ( com wcel w3a coa co nnaord wceq nnacom ancoms 3adant2 3adant1 eleq12d
    bitrd ) ADEZBDEZCDEZFZABECAGHZCBGHZEACGHZBCGHZEABCITUAUCUBUDQSUAUCJZRSQUECA
    KLMRSUBUDJZQSRUFCBKLNOP $.
    $([9-Nov-02]$)

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Addition of natural numbers is associative.  (This can be extended to
       all ordinals with a longer proof.)  Theorem 4K(1) of [Enderton] p. 81. $)
    nnaass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A +o B ) +o C ) = ( A +o ( B +o C ) ) ) $=
      ( vx vy com wcel coa co wceq wi wa cv c0 csuc opreq2 opreq2d cleq12d
      bilimd weq nnacl nna0 syl adantl eqtr4d nnasuc sylan sylan5 eqtrd
      anassrs suceq syl5bir exp comm a2d finds 3imp ) AFGZBFGZCFGZABHIZCHIZABCH
      IZHIZJZURUSUTVEKUTURUSLZVEVFVADMZHIZABVGHIZHIZJZKVFVANHIZABNHIZHIZJZKVFVA
      EMZHIZABVPHIZHIZJZKVFVAVPOZHIZABWAHIZHIZJZKVFVEKDECVGNJZVKVOVFWFVHVLVJVNV
      GNVAHPWFVIVMAHVGNBHPQRSDETZVKVTVFWGVHVQVJVSVGVPVAHPWGVIVRAHVGVPBHPQRSVGWA
      JZVKWEVFWHVHWBVJWDVGWAVAHPWHVIWCAHVGWABHPQRSVGCJZVKVEVFWIVHVBVJVDVGCVAHPW
      IVIVCAHVGCBHPQRSVFVLVAVNVFVAFGZVLVAJABUAZVAUBUCUSVNVAJURUSVMBAHBUBQUDUEVP
      FGZVFVTWEVFWLVTWEKZVFWLWMVFWLLZWEVQOZVSOZJVTWNWBWOWDWPWJWLWBWOJVFVAVPUFWK
      UGURUSWLWDWPJURUSWLLZLWDAVROZHIZWPWQWDWSJURWQWCWRAHBVPUFQUDURVRFGWSWPJWQA
      VRUFBVPUAUHUIUJRVQVSUKULUMUNUOUPUNUMUQ $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Distributive law for natural numbers.  (This can be extended to
       all ordinals with a longer proof.)  Theorem 4K(3) of [Enderton] p. 81. $)
    nndist $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A .o ( B +o C ) ) = ( ( A .o B ) +o ( A .o C ) ) ) $=
      ( vx vy com wcel coa co comu wceq wa cv wi c0 csuc opreq2 opreq2d
      cleq12d bilimd weq nnmcl nna0 syl con0 pm3.26 nnont om0 3syl adantl
      3eqtr4rd w3a nnasuc 3adant1 nnmsuc nnacl sylan5 3impb eqtrd 3adant2
      nnaass syl3an1 syl3an2 3exp exp4b pm2.43a com4r pm2.43i 3imp eqtr4d
      opreq1 syl5bir com3r imp3a a2d finds exp3a com3l ) AFGZBFGZCFGZABCHIZJIZA
      BJIZACJIZHIZKZWAVSVTWGWAVSVTWGVSVTLZABDMZHIZJIZWDAWIJIZHIZKZNWHABOHIZJIZW
      DAOJIZHIZKZNWHABEMZHIZJIZWDAWTJIZHIZKZNWHABWTPZHIZJIZWDAXFJIZHIZKZNWHWGND
      ECWIOKZWNWSWHXLWKWPWMWRXLWJWOAJWIOBHQRXLWLWQWDHWIOAJQRSTDEUAZWNXEWHXMWKXB
      WMXDXMWJXAAJWIWTBHQRXMWLXCWDHWIWTAJQRSTWIXFKZWNXKWHXNWKXHWMXJXNWJXGAJWIXF
      BHQRXNWLXIWDHWIXFAJQRSTWICKZWNWGWHXOWKWCWMWFXOWJWBAJWICBHQRXOWLWEWDHWICAJ
      QRSTWHWDOHIZWDWRWPWHWDFGZXPWDKABUBZWDUCUDWHWQOWDHWHVSAUEGWQOKVSVTUFAUGAUH
      UIRWHWOBAJVTWOBKVSBUCUJRUKWTFGZWHXEXKXSVSVTXEXKNZVSVTXSXTVSVTXSXTVSVTXSUL
      ZXKXBAHIZXDAHIZKXEYAXHYBXJYCYAXHAXAPZJIZYBYAXGYDAJVTXSXGYDKVSBWTUMUNRVSVT
      XSYEYBKZVSXAFGYFVTXSLAXAUOBWTUPUQURUSYAXJWDXCAHIZHIZYCYAXIYGWDHVSXSXIYGKV
      TAWTUOUTRVSVTXSYCYHKZVSVTXSYINNVSVTXSVSYIVTVSXSVSYINZNVSVTVSXSYJWHVSXSLZV
      SYIWHXCFGZVSYIYKXQYLVSYIWHWDXCAVAXRVBAWTUBVCVDVEVFVGVHVIVJSXBXDAHVKVLVDVM
      VNVOVPVQVRVI $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Multiplication of natural numbers is associative.  (This can be extended
       to all ordinals with a longer proof.)  Theorem 4K(4) of [Enderton]
       p. 81. $)
    nnmass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A .o B ) .o C ) = ( A .o ( B .o C ) ) ) $=
      ( vx vy com wcel comu co wceq wa cv wi c0 csuc opreq2 opreq2d cleq12d
      bilimd weq nnmcl nnm0 syl sylan9eqr eqtr4d w3a coa nnmsuc sylan 3impa
      3adant1 nndist syl3an2 3exp exp3a com34 pm2.43d 3imp eqtrd opreq1
      syl5bir com3r imp3a a2d finds com3l ) AFGZBFGZCFGZABHIZCHIZABCHIZHIZJZVIV
      GVHVNVIVGVHVNVGVHKZVJDLZHIZABVPHIZHIZJZMVOVJNHIZABNHIZHIZJZMVOVJELZHIZABW
      EHIZHIZJZMVOVJWEOZHIZABWJHIZHIZJZMVOVNMDECVPNJZVTWDVOWOVQWAVSWCVPNVJHPWOV
      RWBAHVPNBHPQRSDETZVTWIVOWPVQWFVSWHVPWEVJHPWPVRWGAHVPWEBHPQRSVPWJJZVTWNVOW
      QVQWKVSWMVPWJVJHPWQVRWLAHVPWJBHPQRSVPCJZVTVNVOWRVQVKVSVMVPCVJHPWRVRVLAHVP
      CBHPQRSVOWANWCVOVJFGZWANJABUAZVJUBUCVHVGWCANHINVHWBNAHBUBQAUBUDUEWEFGZVOW
      IWNXAVGVHWIWNMZVGVHXAXBVGVHXAXBVGVHXAUFZWNWFVJUGIZWHVJUGIZJWIXCWKXDWMXEVG
      VHXAWKXDJZWSXAXFVOVJWEUHWTUIUJXCWMAWGBUGIZHIZXEXCWLXGAHVHXAWLXGJVGBWEUHUK
      QVGVHXAXHXEJZVGVHXAXIMVGVHXAVHXIVGVHXAVHXIMVGVHXAKZVHXIVGWGFGVHXIXJAWGBUL
      BWEUAUMUNUOUPUQURUSRWFWHVJUGUTVAUNVBVCVDVEUOVFUR $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Multiplication with successor.  Exercise 16 of [Enderton] p. 82. $)
    nnmsucr $p |- ( ( A e. om /\ B e. om ) ->
                  ( suc A .o B ) = ( ( A .o B ) +o B ) ) $=
      ( vx vy com wcel csuc comu co coa wceq cv wi c0 opreq2 id opreq12d
      cleq12d bilimd weq peano2b nnm0 sylbi opreq1d peano1 nna0 ax-mp syl6eq
      eqtr4d wa nnmsuc sylanb nnacom suceq syl nnasuc ancoms 3eqtr4d opreq2d
      nnaass syl3an3b nnmcl syl3an1 3expb anidms an41r3s eqtrd opreq1 syl5bir
      exp a2d finds imp ) BEFZAEFZAGZBHIZABHIZBJIZKZVNVOVTVOVPCLZHIZAWAHIZWAJIZ
      KZMVOVPNHIZANHIZNJIZKZMVOVPDLZHIZAWJHIZWJJIZKZMVOVPWJGZHIZAWOHIZWOJIZKZMV
      OVTMCDBWANKZWEWIVOWTWBWFWDWHWANVPHOWTWCWGWANJWANAHOWTPQRSCDTZWEWNVOXAWBWK
      WDWMWAWJVPHOXAWCWLWAWJJWAWJAHOXAPQRSWAWOKZWEWSVOXBWBWPWDWRWAWOVPHOXBWCWQW
      AWOJWAWOAHOXBPQRSWABKZWEVTVOXCWBVQWDVSWABVPHOXCWCVRWABJWABAHOXCPQRSVOWFNW
      HVOVPEFZWFNKAUAZVPUBUCVOWHNNJIZNVOWGNNJAUBUDNEFXFNKUENUFUGUHUIWJEFZVOWNWS
      XGVOWNWSMZVOXGXHVOXGUJZWSWKVPJIZWMVPJIZKWNXIWPXJWRXKXDXGWPXJKVOVPWJUKXEUL
      XIWRWLAJIZWOJIZXKXIWQXLWOJAWJUKUDXIWLAWOJIZJIZWLWJVPJIZJIZXMXKXIXNXPWLJXI
      AWJJIZGZWJAJIZGZXNXPXIXRXTKXSYAKAWJUMXRXTUNUOAWJUPXGVOXPYAKWJAUPUQURUSXIX
      MXOKZXIVOXGYBWLEFZVOXGYBXIYCVOWOEFYBXGWLAWOUTWJUAVAAWJVBZVCVDVEXIXKXQKZVO
      XGXGVOYEXIXGVOYEYCXGVOYEXIYCXGXDYEVOWLWJVPUTXEVAYDVCVDVFVEURVGRWKWMVPJVHV
      IUQVJVKVLVMUQ $.
      $([21-Sep-95]$)
  $}

  ${
    $d x A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Multiplication of natural numbers is commutative.  Theorem 4K(5) of
       [Enderton] p. 81. $)
    nnmcom $p |- ( ( A e. om /\ B e. om ) -> ( A .o B ) = ( B .o A ) ) $=
      ( vx vy com wcel comu co wceq cv wi c0 csuc opreq1 opreq2 cleq12d bilimd
      weq nnm0r nnm0 eqtr4d wa coa nnmsucr nnmsuc ancoms syl5bir exp a2d finds
      imp ) AEFBEFZABGHZBAGHZIZULCJZBGHZBUPGHZIZKULLBGHZBLGHZIZKULDJZBGHZBVCGHZ
      IZKULVCMZBGHZBVGGHZIZKULUOKCDAUPLIZUSVBULVKUQUTURVAUPLBGNUPLBGOPQCDRZUSVF
      ULVLUQVDURVEUPVCBGNUPVCBGOPQUPVGIZUSVJULVMUQVHURVIUPVGBGNUPVGBGOPQUPAIZUS
      UOULVNUQUMURUNUPABGNUPABGOPQULUTLVABSBTUAVCEFZULVFVJVOULVFVJKVOULUBZVJVDB
      UCHZVEBUCHZIVFVPVHVQVIVRVCBUDULVOVIVRIBVCUEUFPVDVEBUCNUGUHUIUJUK $.
      $([21-Sep-95]$)
  $}

  $( Cancellation law for addition of natural numbers. $)
  nnacan $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                   ( ( A +o B ) = ( A +o C ) <-> B = C ) ) $=
    ( com wcel w3a coa co wo wn wceq wb nnaord 3comr 3com13 bi2ord binegd
    bicomd word wa ordtri3 nnacl nnord syl syl2an 3impdi 3adant1 3bitr4d ) ADEZ
    BDEZCDEZFZABGHZACGHZEZUNUMEZIZJZBCEZCBEZIZJZUMUNKZBCKZULVBURULVAUQULUSUOUTU
    PUJUKUIUSUOLBCAMNUKUJUIUTUPLCBAMOPQRUIUJUKVCURLZUMSZUNSZVEUIUJTZUIUKTZUMUNU
    AVHUMDEVFABUBUMUCUDVIUNDEVGACUBUNUCUDUEUFUJUKVDVBLZUIBSCSVJUJUKBCUABUCCUCUE
    UGUH $.
    $([27-Oct-95]$)

  $( Weak ordering property of addition. $)
  nnaword $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                ( A (_ B <-> ( C +o A ) (_ ( C +o B ) ) ) $=
    ( com wcel w3a wceq wo coa co wss nnaord wb nnacan 3coml bicomd bi2ord
    con0 wa nnont im2an 3adant3 onsseleq syl anandi nnacl sylbi ancoms 3impa
    3bitr4d ) ADEZBDEZCDEZFZABEZABGZHZCAIJZCBIJZEZURUSGZHZABKZURUSKZUNUOUTUPVAA
    BCLUNVAUPUMUKULVAUPMCABNOPQUNAREZBREZSZVCUQMUKULVGUMUKVEULVFATBTUAUBABUCUDU
    NURREZUSREZSZVDVBMUKULUMVJUMUKULSZVJUMVKSUMUKSZUMULSZSVJUMUKULUEVLVHVMVIVLU
    RDEVHCAUFURTUDVMUSDEVICBUFUSTUDUAUGUHUIURUSUCUDUJ $.
    $([17-Sep-95]$)

  $( Weak ordering property of addition. $)
  nnaword1 $p |- ( ( A e. om /\ B e. om ) -> A (_ ( A +o B ) ) $=
    ( com wcel wa c0 coa co wss nullss wb peano1 nnaword 3com13 mp3an3 mpbii
    wceq nna0 adantr sseq1d mpbid ) ACDZBCDZEZAFGHZABGHZIZAUFIUDFBIZUGBJUBUCFCD
    ZUHUGKZLUIUCUBUJFBAMNOPUDUEAUFUBUEAQUCARSTUA $.
    $([9-Nov-02]$)

  $( Weak ordering property of addition. $)
  nnaword2 $p |- ( ( A e. om /\ B e. om ) -> A (_ ( B +o A ) ) $=
    ( com wcel wa coa co wss nnaword1 nnacom sseq2d mpbid ) ACDBCDEZAABFGZHABAF
    GZHABIMNOAABJKL $.
    $([9-Nov-02]$)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Ordering property of multiplication.  Half of Proposition 8.19 of
       [TakeutiZaring] p. 63, limited to natural numbers. $)
    nnmordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A e. B /\ (/) e. C ) -> ( C .o A ) e. ( C .o B ) ) ) $=
      ( vx vy com wcel w3a c0 comu co csuc wss wi wa cv wceq opreq2 sseq2d
      bilimd weq ssid a1i coa sstr peano1 nullss nnaword mpbii mp3an1 con0
      pm3.27 nnont oa0 3syl sseq1d mpbid nnmcl sylan5 anabss5 syl5 comm nnmsuc
      sylibrd exp ad2antll a2d findsg peano2b sylan5b word ordsucss imp nnord
      sylan exp4b pm2.43b com34 3imp ssel syl6bi nnaordi eleq1d sylibd syl5d
      ancoms 3adant2 syld imp3a ) AFGZBFGZCFGZHZABGZICGZCAJKZCBJKZGZWMWNCALZJKZ
      WQMZWOWRNZWJWKWLWNXANWJWKWNWLXAWJWKWNWLXANZNWKWJWKWNXCWKWJOWSBMZXCWKWNOWK
      WSFGZXDXCNWJWKXEOXDXCWLWTCDPZJKZMZNWLWTWTMZNZWLWTCEPZJKZMZNWLWTCXKLZJKZMZ
      NXCDEBWSXFWSQZXHXIWLXQXGWTWTXFWSCJRSTDEUAZXHXMWLXRXGXLWTXFXKCJRSTXFXNQZXH
      XPWLXSXGXOWTXFXNCJRSTXFBQZXHXAWLXTXGWQWTXFBCJRSTXJXEXIWLWTUBUCUCXKFGZXEOW
      SXKMZOWLXMXPYAWLXMXPNZNXEYBWLYAYCWLYAYCWLYAOZXMWTXLCUDKZMZXPXMYDYFXMXLYEM
      ZYFYDWTXLYEUEWLYAYGWLXLFGZYGYDWLYHOZXLIUDKZYEMZYGIFGZWLYHYKUFYLWLYHHICMYK
      CUGICXLUHUIUJYIYJXLYEYIYHXLUKGYJXLQWLYHULXLUMXLUNUOUPUQCXKURUSUTVAVBYDXOY
      EWTCXKVCSVDVEVBVFVGVHVEAVIVJBVKZWNXDWKYMWNXDABVLVMBVNVOVAVPVQVRVSWJWLXAXB
      NZWKWLWJYNWLWJOZXAWPWPCUDKZGZWRWOYOXAYPWQMYQWRNYOWTYPWQCAVCUPYPWQWPVTWAWL
      WJWOYQNZWLWPFGZYRYOWLYSOZWOWPIUDKZYPGYQICWPWBYTUVAWPYPYTYSWPUKGUVAWPQWLYS
      ULWPUMWPUNUOWCWDCAURUSUTWEWFWGWHWI $.
      $([18-Sep-95]$)
  $}

  $( Ordering property of multiplication.  Proposition 8.19 of
     [TakeutiZaring] p. 63, limited to natural numbers. $)
  nnmord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) <-> ( C .o A ) e. ( C .o B ) ) ) $=
    ( com wcel w3a c0 wa comu co nnmordi wi wceq wo wn opreq2 a1i exp3a com23
    3com12 imp im2ord pm2.16d word wb pm3.26 df-3an ancom anandi 3bitr sylib
    nnmcl nnord syl im2an ordtri2 3syl 3simpa 3imtr4d exp ancr syl6 opreq1
    cleq1d nnm0r syl5bir comm noel2 nsyli adantr ord0eln0 adantl sylibrd
    3adant1 syl5d pm2.43d impbid ) ADEZBDEZCDEZFZABEZGCEZHZCAIJZCBIJZEZABCKWAWG
    WDWAWGWCWDWGWAWGWCWBLWCWDLWAWCWGWBWAWCWGWBLWAWCHZWEWFMZWFWEEZNZOZABMZBAEZNZ
    OZWGWBWHWOWKWHWMWIWNWJWMWILWHABCIPQWAWCWNWJLZVSVRVTWCWQLVSVRVTFZWNWCWJWRWNW
    CWJBACKRSTUAUBUCWHVTVRHZVTVSHZHZWEUDZWFUDZHWGWLUEWHWAXAWAWCUFZWAVRVSHZVTHVT
    XEHXAVRVSVTUGXEVTUHVTVRVSUIUJUKWSXBWTXCWSWEDEXBCAULWEUMUNWTWFDEXCCBULWFUMUN
    UOWEWFUPUQWHAUDZBUDZHZWBWPUEWHWAXEXHXDVRVSVTURVRXFVSXGAUMBUMUOUQABUPUNUSUTS
    WCWBVAVBVSVTWGWCLVRVSVTHWGCGMZOZWCVSWGXJLVTVSXIWFGMZWGXIVSXKXIXKGBIJZGMVSXI
    WFXLGCGBIVCVDBVEVFVGWFWEVHVIVJVTWCXJUEZVSVTCUDXMCUMCVKUNVLVMVNVOVPVQ $.
    $([22-Jan-96]$)

  $( Cancellation law for multiplication of natural numbers. $)
  nnmcan $p |- ( ( ( A e. om /\ B e. om /\ C e. om ) /\ (/) e. A ) ->
                   ( ( A .o B ) = ( A .o C ) <-> B = C ) ) $=
    ( com wcel w3a c0 wa comu co wceq wo wn wi nnmordi 3comr 3com13 im2ord
    andir syl5ib exp3a com23 imp pm2.16d wb word ordtri3 nnmcl nnord syl
    syl2an 3impdi adantr 3adant1 3imtr4d opreq2 a1i impbid ) ADEZBDEZCDEZFZGAEZ
    HZABIJZACIJZKZBCKZVDVEVFEZVFVEEZLZMZBCEZCBEZLZMZVGVHVDVOVKVBVCVOVKNVBVOVCVK
    VBVOVCVKVBVMVCHZVNVCHZLVKVOVCHVBVQVIVRVJUTVAUSVQVINBCAOPVAUTUSVRVJNCBAOQRVM
    VNVCSTUAUBUCUDVBVGVLUEZVCUSUTVAVSVEUFZVFUFZVSUSUTHZUSVAHZVEVFUGWBVEDEVTABUH
    VEUIUJWCVFDEWAACUHVFUIUJUKULUMVBVHVPUEZVCUTVAWDUSBUFCUFWDUTVABCUGBUICUIUKUN
    UMUOVHVGNVDBCAIUPUQUR $.
    $([26-Oct-95]$)

  ${
    $d x y z A $.  $d x y z B $.  $( ` y z ` are dummy. $)
    nnordexlem.1 $e |- A e. om $.
    nnordexlem.2 $e |- B e. om $.
    $( Equivalence for ordering.  Compare Exercise 23 of [Enderton] p. 88. $)
    nnordexlem   $p |- ( A e. B <->
                       E. x e. om ( (/) e. x /\ ( A +o x ) = B ) ) $=
      ( vy vz wcel c0 cv coa co wceq wa com wrex wex wss wel wn wi wal cab cin
      ssab peano1 w3a nullss nnaword mpbii mp3an nna0 ax-mp nnacom mp2an
      3sstr3 elisseti eleq1 opreq2 sseq2d bi2and cla4ev noel3 mpbir word ordom
      tz7.5 mpan visset weq elab disj birim bial bitr bi2an biex mpbi nnaordi
      mpan2 eleq1d sylibd opreq1 nna0r syl6eq eleq2d biimpcd pm2.16d syl6 wb
      nnacl nnord ordtri1 3syl biimpd syl5d com23 imp adantr ord0eln0 syl
      pm5.32i biimpr exp ad2antll csuc nnsuc ax-17 hba1 hbim peano2b syl6bbr
      con0 nnont nnon onsssuc adantl nnasuc sylan9eq bitr4d biland birimd
      biimpa exp4b pm2.03 syl8 com4t sucid eleq2 mpbiri syl5 comm pm2.43i
      sylbi a4sd 19.23ai pm2.43a wo ordsseleq ord cleqcom syl6ib syl6d jcad
      syld 19.22dv mpi anass sylib df-rex sylibr eleq1i syl6bb syl5bi imp3a
      r19.23aiv impbi ) BCHZIAJZHZBUVSKLZCMZNZAOPZUVRUVSOHZUWCNZAQZUWDUVRUWEUVT
      NZUWBNZAQZUWGUVRUWECUWARZNZFJZOHZCBUWMKLZRZNZFASZTZUAZFUBZNZAQZUWJUVSGJZO
      HZCBUXDKLZRZNZGUCZHZUXIUVSUDIMZNZAQZUXCUXIORZUXIIMTZUXMUXGGOUEUXOUXHGQZCO
      HZCBCKLZRZUXPECIKLZCBKLZCUXRIOHZBOHZUXQUXTUYARZUFDEUYBUYCUXQUGIBRUYDBUHIB
      CUIUJUKUXQUXTCMECULUMUXQUYCUYAUXRMEDCBUNUOUPUXHUXQUXSNGCCOEUQUXDCMZUXEUXQ
      UXGUXSUXDCOURUYEUXFUXRCUXDCBKUSUTVAVBUOUXHGVCVDOVEUXNUXONUXMVFAOUXIVGVHUO
      UXLUXBAUXJUWLUXKUXAUXHUWLGUVSAVIGAVJZUXEUWEUXGUWKUXDUVSOURUYFUXFUWACUXDUV
      SBKUSUTVAVKUXKUWMUXIHZUWSUAZFUBUXAFUXIUVSVLUYHUWTFUYGUWQUWSUXHUWQGUWMFVIZ
      GFVJZUXEUWNUXGUWPUXDUWMOURUYJUXFUWOCUXDUWMBKUSUTVAVKVMVNVOVPVQVRUVRUXBUWI
      AUXBUVRUWIUXBUVRUVSIMZTZUWIUWLUVRUYLUAZUXAUWEUWKUYMUWEUVRUWKUYLUWEUVRUWAC
      HZTZUYLUWKUWEUVRUWAUVSCKLZHZUYOUYLUAUWEUVRUVSBKLZUYPHZUYQUXQUWEUVRUYSUAEB
      CUVSVSVHUWEUYRUWAUYPUWEUYCUYRUWAMDUVSBUNVTWAWBUYQUYKUYNUYKUYQUYNUYKUYPCUW
      AUYKUYPICKLZCUVSICKWCUXQUYTCMECWDUMWEWFWGWHWIUWEUWKUYOUWEUWAOHZUWAVEZUWKU
      YOWJZUYCUWEVUADBUVSWKVHZUWAWLZCVEZVUBVUCUXQVUFECWLUMZCUWAWMVHWNWOWPWQWRWS
      UXBUYLUWHUWBUWEUYLUWHUAUWKUXAUWEUYLUWHUWHUWEUYLNUWEUVTUYLUWEUVSVEUVTUYLWJ
      UVSWLUVSWTXAXBXCXDXEUWLUXAUYLUWBUAUWLUYLUXAUWBUWLUYLUXACUWAHZTZUWBUWEUYLU
      XAVUIUAZUAUWKUYLUWEVUJUWEUYLUVSUWMXFZMZFQZUWEVUJUAZUWEUYLVUMFUVSXGXDVULVU
      NFUWEVUJFUWEFXHUXAVUIFUWTFXIVUIFXHXJXJVULUWEVUJVULUWENZUWTVUIFVUOVULUWNNZ
      UWTVUIUAZVULUWEUWNVULUWEVUKOHUWNUVSVUKOURUWMXKXLXBVUPVUQVULUWNVUPVUQUAZUW
      NVULVURUWNUWRVURVULVUPUWTUWNUWRVUIVUPUWTUWNVUHUWSUAUWRVUIUAVUPUWTUWNVUHUW
      SVUPUWTUWNVUHNZUWSUAVUPUWQVUSUWSVUPUWPVUHUWNVUPUWPCUWOXFZHZVUHUWNUWPVVAWJ
      ZVULUWNUWOOHZUWOXMHZVVBUYCUWNVVCDBUWMWKVHUWOXNCXMHVVDVVBCEXOCUWOXPVHWNXQV
      UPUWAVUTCVULUWNUWABVUKKLZVUTUVSVUKBKUSUYCUWNVVEVUTMDBUWMXRVHXSWFXTYAYBYCY
      DVUHUWRYEYFYGVULUWRUWMVUKHUWMUYIYHUVSVUKUWMYIYJYKYLWRYMYNYOXDYPWIYQWSUWLV
      UICUWAMZUWBUWLVUHVVFUWEUWKVUHVVFYRZUWEVUAVUBUWKVVGWJZVUDVUEVUFVUBVVHVUGCU
      WAYSVHWNYCYTCUWAUVAUVBUVCWQWRUVDUVEYLUVFUVGUWIUWFAUWEUVTUWBUVHVQUVIUWCAOU
      VJUVKUWCUVRAOUWEUVTUWBUVRUWEUVTUWBUVRUAUWBUWHUVRUWBBIKLZUWAHZUVRUWHUWBVVJ
      VVICHUVRUWACVVIYIVVIBCUYCVVIBMDBULUMUVLUVMUWEUVTVVJUWEUYCUVTVVJUADIUVSBVS
      VTWRUVNYLXDUVOUVPUVQ $.
      $([12-Jan-96]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for ordering.  Compare Exercise 23 of [Enderton] p. 88. $)
    nnordex $p |- ( ( A e. om /\ B e. om ) -> ( A e. B <->
                  E. x e. om ( (/) e. x /\ ( A +o x ) = B ) ) ) $=
      ( com wcel c0 cv coa co wceq wa wrex wb cded eleq1 opreq1 cleq1d biland
      birexdv bi2bid eleq2 cleq2 peano1 elimel nnordexlem dedth2h ) BDEZCDEZBCE
      ZFAGZEZBUJHIZCJZKZADLZMUGBFNZCEZUKUPUJHIZCJZKZADLZMUPUHCFNZEZUKURVBJZKZAD
      LZMBCFFBUPJZUIUQUOVABUPCOVGUNUTADVGUMUSUKVGULURCBUPUJHPQRSTCVBJZUQVCVAVFC
      VBUPUAVHUTVEADVHUSVDUKCVBURUBRSTAUPVBBFDUCUDCFDUCUDUEUF $.
      $([5-Dec-95]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for weak ordering of natural numbers. $)
    nnwordex $p |- ( ( A e. om /\ B e. om ) -> ( A (_ B <->
                  E. x e. om ( A +o x ) = B ) ) $=
      ( com wcel wa wss c0 cv coa co wceq wrex wo con0 wb onsseleq nnont
      syl2an nnordex nna0 cleq1d wex zfnull opreq2 ceqsexv cleqcom peano1
      eleq1 mpbii pm4.71ri bitr biran anass biex df-rex bitr4 bitr3 syl5rbbr
      adantr bi2ord bitrd nullss 0elon jctil syl biantrurd andir syl6bb birexa
      r19.43 bitr2 ) BDEZCDEZFZBCGZHAIZEZBVQJKZCLZFZADMZHVQLZVTFZADMZNZVTADMZVO
      VPBCEZBCLZNZWFBOECOEVPWJPVMVNBCQBRCRSVOWHWBWIWEABCTVMWIWEPVNVMBHJKZCLZWIW
      EVMWKBCBUAUBWLVQHLZVTFZAUCZWEVTWLAHUDWMVSWKCVQHBJUEUBUFWOVQDEZWDFZAUCWEWN
      WQAWNWPWCFZVTFWQWMWRVTWMWCWRVQHUGWCWPWCHDEWPUHHVQDUIUJUKULUMWPWCVTUNULUOW
      DADUPUQURUSUTVAVBWGWAWDNZADMWFVTWSADWPVTVRWCNZVTFWSWPWTVTWPHVQGZWTVQVCWPH
      OEZVQOEZFXAWTPWPXCXBVQRVDVEHVQQVFUJVGVRWCVTVHVIVJWAWDADVKVLVI $.
      $([8-Nov-02]$)
  $}

  $( One is a natural number. $)
  1nn $p |- 1o e. om $=
    ( c1o c0 csuc com df-1o wcel peano1 peano2 ax-mp eqeltr ) ABCZDEBDFKDFGBHIJ
    $.
    $([29-Oct-95]$)

  ${
    $d x y z w v A $.  $d x y z w v F $.
    $( Lemma for ~ omsmo . $)
    omsmolem $p |- ( y e. om -> ( ( ( A (_ On /\ F : om --> A ) /\
        A. x e. om ( F ` x ) e. ( F ` suc x ) ) ->
       ( z e. y -> ( F ` z ) e. ( F ` y ) ) ) ) $=
      ( vw wel cv cfv wcel wi c0 csuc con0 wss com wf wa wral wceq eleq2 fveq2
      eleq2d bi2imd weq noel pm2.21i a1i wo suceq fveq2d eleq12d rcla4v imp
      adantll ssel ffvrn peano2b sylan5b syl5 ontr exp3a com23 syl6 imp31
      adantlr mpd syl3d eleq1d syl5bir comm adantr jaod visset elsuc syl5ib
      exp31 finds2 ) CBGZCHZEIZBHZEIZJZKVTLJZWALEIZJZKZCFGZWAFHZEIZJZKZVTWJMZJZ
      WAWNEIZJZKZDNOZPDEQZRZAHZEIZXBMZEIZJZAPSZRZBFWBLTZVSWEWDWGWBLVTUAXIWCWFWA
      WBLEUBUCUDBFUEZVSWIWDWLWBWJVTUAXJWCWKWAWBWJEUBUCUDWBWNTZVSWOWDWQWBWNVTUAX
      KWCWPWAWBWNEUBUCUDWHXHWEWGVTUFUGUHXHWJPJZWMWRKXHXLWMWRXHXLRZWMRZWICFUEZUI
      WQWOXNWIWQXOXMWMWIWQKXMWLWQWIXMWKWPJZWLWQKZXGXLXPXAXGXLXPXFXPAWJPAFUEZXCW
      KXEWPXBWJEUBXRXDWNEXBWJUJUKULUMUNZUOXAXLXPXQKZXGWSWTXLXTWSWTXLXTWSWTXLRZW
      PNJZXTWSWPDJZYBYADNWPUPWTWNPJYCXLPDWNEUQWJURUSUTYBWLXPWQYBWLXPWQWAWKWPVAV
      BVCVDVBVEVFVGVHUNXMXOWQKZWMXGXLYDXAXOXGXLRZWQXOWQXPYEXOWAWKWPVTWJEUBVIXSV
      JVKUOVLVMVTWJCVNVOVPVQVKVR $.
      $([?]$)

    $( A strictly monotonic ordinal function on the set of natural numbers
       is one-to-one. $)
    omsmo $p |- ( ( ( A (_ On /\ F : om --> A ) /\
          A. x e. om ( F ` x ) e. ( F ` suc x ) ) -> F : om -1-1-> A ) $=
      ( vy vz con0 wss com wf wa cv cfv csuc wcel wral wceq weq wi wf1 pm3.27
      adantr wo wn wel omsmolem adantl imp im2ord ancoms pm2.16d wb word ssel
      ffvrn syl5 exp3a eloni syl6 im2and ordtri3 syl adantlr nnord syl2an
      3imtr4d exp32 r19.21adv r19.21aiv jca f1fv sylibr ) BFGZHBCIZJZAKZCLVOMCL
      NAHOZJZVMDKZCLZEKZCLZPZDEQZRZEHOZDHOZJHBCSVQVMWFVNVMVPVLVMTUAVQWEDHVQVRHN
      ZWDEHVQWGVTHNZWDVQWGWHJZJZVSWANZWAVSNZUBZUCZDEUDZEDUDZUBZUCZWBWCWJWQWMWIV
      QWQWMRWIVQJWOWKWPWLWIVQWOWKRZWHVQWSRWGAEDBCUEUFUGWIVQWPWLRZWGVQWTRWHADEBC
      UEUAUGUHUIUJVNWIWBWNUKZVPVNWIJVSULZWAULZJZXAVNWIXDVNWGXBWHXCVNWGVSFNZXBVL
      VMWGXERVLVMWGXEVLVSBNXEVMWGJBFVSUMHBVRCUNUOUPUGVSUQURVNWHWAFNZXCVLVMWHXFR
      VLVMWHXFVLWABNXFVMWHJBFWAUMHBVTCUNUOUPUGWAUQURUSUGVSWAUTVAVBWIWCWRUKZVQVR
      ULVTULXGWGWHVRVTUTVRVCVTVCVDUFVEVFVGVHVIDEHBCVJVK $.
      $([?]$)
  $}


