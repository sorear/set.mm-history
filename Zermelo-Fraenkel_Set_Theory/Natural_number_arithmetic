$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Addition with zero.  Theorem 4I(A1) of Enderton, p. 79. $)
  nna0 $p |- ( A e. om -> ( A +_o (/) ) = A ) $=
    ( com wcel con c0 coa co wceq nnont oa0 syl ) ABCADCAEFGAHAIAJK $.
    $([20-Sep-95]$)

  $( Multiplication with zero.  Theorem 4J(A1) of Enderton, p. 80. $)
  nnm0 $p |- ( A e. om -> ( A ._o (/) ) = (/) )  $=
    ( com wcel con c0 comu co wceq nnont om0 syl ) ABCADCAEFGEHAIAJK $.
    $([20-Sep-95]$)

  $( Addition with successor.  Theorem 4I(A2) of Enderton, p. 79. $)
  nnasuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A +_o suc B ) = suc ( A +_o B ) ) $=
    ( con wcel csuc coa co wceq com oasuc nnont syl2an ) ACDBCDABEFGABFGEHAIDBI
    DABJAKBKL $.
    $([20-Sep-95]$)

  $( Multiplication with successor.  Theorem 4J(A2) of Enderton, p. 80. $)
  nnmsuc $p |- ( ( A e. om /\ B e. om ) ->
                ( A ._o suc B ) = ( ( A ._o B ) +_o A ) ) $=
    ( con wcel csuc comu co coa wceq com omsuc nnont syl2an ) ACDBCDABEFGABFGAH
    GIAJDBJDABKALBLM $.
    $([20-Sep-95]$)

  $( Addition to zero.  Enderton, p. 81. $)
  nna0r $p |- ( A e. om -> ( (/) +_o A ) = A ) $=
    ( com wcel con c0 coa co wceq nnont oa0r syl ) ABCADCEAFGAHAIAJK $.
    $([20-Sep-95]$)

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Multiplication with zero.  Enderton, p. 82. $)
    nnm0r $p |- ( A e. om -> ( (/) ._o A ) = (/) ) $=
      ( vx vy c0 cv comu co wceq csuc opreq2 cleq1d weq com wcel peano1 nnm0
      ax-mp coa nnmsuc mpan nna0 cleqcomi a1i cleq12d opreq1 syl5bir finds ) DB
      EZFGZDHDDFGZDHZDCEZFGZDHZDULIZFGZDHZDAFGZDHBCAUHDHUIUJDUHDDFJKBCLUIUMDUHU
      LDFJKUHUOHUIUPDUHUODFJKUHAHUIURDUHADFJKDMNZUKODPQULMNZUQUMDRGZDDRGZHUNUTU
      PVADVBUSUTUPVAHDULSOTDVBHUTVBDUSVBDHODUAQUBUCUDUMDDRUEUFUG $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers.  Proposition 8.9 of Takeuti and
       Zaring, p. 59. $)
    nnacl $p |- ( ( A e. om /\ B e. om ) -> ( A +_o B ) e. om ) $=
      ( vx vy com wcel coa co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq nna0
      biimprd pm2.43i wa nnasuc peano2b biimp syl5bir exp comm a2d finds imp )
      AEFZBEFZABGHZEFZULUKUNUKACIZGHZEFZJUKAKGHZEFZJUKADIZGHZEFZJUKAUTLZGHZEFZJ
      UKUNJCDBUOKMZUQUSUKVFUPUREUOKAGNOPCDQZUQVBUKVGUPVAEUOUTAGNOPUOVCMZUQVEUKV
      HUPVDEUOVCAGNOPUOBMZUQUNUKVIUPUMEUOBAGNOPUKUSUKUSUKUKURAEAROSTUTEFZUKVBVE
      UKVJVBVEJZUKVJVKUKVJUAZVEVALZEFZVBVLVDVMEAUTUBOVBVNVAUCUDUEUFUGUHUIUGUJ
      $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmcl $p |- ( ( A e. om /\ B e. om ) -> ( A ._o B ) e. om ) $=
      ( vx vy com wcel comu co cv wi c0 csuc wceq opreq2 eleq1d bilimd weq
      nnm0 peano1 mpbiri wa coa nnmsuc nnacl syl5bir exp4b com24 pm2.43i com3r
      a2d finds comm imp ) AEFZBEFZABGHZEFZUOUNUQUNACIZGHZEFZJUNAKGHZEFZJUNADIZ
      GHZEFZJUNAVCLZGHZEFZJUNUQJCDBURKMZUTVBUNVIUSVAEURKAGNOPCDQZUTVEUNVJUSVDEU
      RVCAGNOPURVFMZUTVHUNVKUSVGEURVFAGNOPURBMZUTUQUNVLUSUPEURBAGNOPUNVBKEFUNVA
      KEAROSTVCEFZUNVEVHUNVEVMVHUNVEVMVHJJUNVMVEUNVHUNVMVEUNVHUNVMUAZVHVDAUBHZE
      FVEUNUAVNVGVOEAVCUCOVDAUDUEUFUGUHUIUJUKULUM $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Ordering property of addition.  Half of Proposition 8.4 of Takeuti and
       Zaring, p. 58, limited to natural numbers. $)
    nnaordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B -> ( C +_o A ) e. ( C +_o B ) ) ) $=
      ( vx vy com wcel con coa co wi w3a csuc wss wa cv wceq opreq2 sseq2d
      bilimd weq ssid a1i oasuc ancoms sstr tz7.23b mpi syl5bir nnont sylan
      exp adantr a2d findsg peano2b sylan5b word ordsucssi imp nnord syl5
      exp4b pm2.43b com34 3imp sseq1d cvv oprex onsucssir ax-mp syl6bi sylan5
      3adant2 syld 3sylan3 ) AFGZBFGZCHGZABGZCAIJZCBIJZGZKCFGVQVRVSLVTCAMZIJZWB
      NZWCVQVRVSVTWFKVQVRVTVSWFVQVRVTVSWFKZKVRVQVRVTWGVRVQOWDBNZWGVRVTOVRWDFGZW
      HWGKVQVRWIOWHWGVSWECDPZIJZNZKVSWEWENZKVSWECEPZIJZNZKVSWECWNMZIJZNZKWGDEBW
      DWJWDQZWLWMVSWTWKWEWEWJWDCIRSTDEUAZWLWPVSXAWKWOWEWJWNCIRSTWJWQQZWLWSVSXBW
      KWRWEWJWQCIRSTWJBQZWLWFVSXCWKWBWEWJBCIRSTWMVSWEUBUCWNFGZWIOZWDWNNZOVSWPWS
      XEVSWPWSKZKZXFXDXHWIXDVSXGWNHGZVSXGXDXIVSOZWSWEWOMZNZWPXJWRXKWEVSXIWRXKQC
      WNUDUESWPWOXKNXLWEWOXKUFWOUGUHUIWNUJUKULUMUMUNUOULAUPUQBURZVTWHVRXMVTWHAB
      USUTBVAUKVBVCVDVEVFVQVSWFWCKZVRVSVQXNVSAHGZXNVQVSXOOZWFWAMZWBNZWCXPWEXQWB
      CAUDVGWAVHGXRWCKCAIVIWAWBVHVJVKVLAUJVMUEVNVOCUJVP $.
      $([16-Sep-95]$)
  $}

  $( Ordering property of addition.  Proposition 8.4 of Takeuti and Zaring,
     p. 58, limited to natural numbers. $)
  nnaord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A e. B <-> ( C +_o A ) e. ( C +_o B ) ) ) $=
    ( com wcel w3a coa co nnaordi wceq wo wn wi opreq2 a1i 3com12 im2ord
    pm2.16d word wa wb df-3an ancom anandi 3bitr nnacl nnord syl im2an sylbi
    ordtri 3simpa 3syl 3imtr4d impbid ) ADEZBDEZCDEZFZABEZCAGHZCBGHZEZABCIUSVAV
    BJZVBVAEZKZLZABJZBAEZKZLZVCUTUSVJVFUSVHVDVIVEVHVDMUSABCGNOUQUPURVIVEMBACIPQ
    RUSVASZVBSZTZVCVGUAUSURUPTZURUQTZTZVNUSUPUQTZURTURVRTVQUPUQURUBVRURUCURUPUQ
    UDUEVOVLVPVMVOVADEVLCAUFVAUGUHVPVBDEVMCBUFVBUGUHUIUJVAVBUKUHUSVRASZBSZTUTVK
    UAUPUQURULUPVSUQVTAUGBUGUIABUKUMUNUO $.
    $([7-Mar-96]$)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Ordering property of addition.  Proposition 8.4 of Takeuti and Zaring,
       p. 58, limited to natural numbers. $)
    nnaordNEW $p |- ( ( B e. om /\ C e. om ) ->
                 ( A e. B -> ( C +_o A ) e. ( C +_o B ) ) ) $=
      ( vx vy com wcel coa co wi wa con w3a csuc wss cv wceq opreq2 sseq2d
      bilimd weq ssid a1i oasuc ancoms sstr tz7.23b mpi syl5bir nnont sylan
      exp adantr a2d findsg peano2b sylan5b word ordsucssi imp nnord syl5
      exp4b pm2.43b com34 3imp sseq1d cvv oprex onsucssir ax-mp syl6bi sylan5
      3adant2 syld 3sylan3 elnn 3sylan1 3exp com4r exp3a pm2.43i pm2.43d com3l
      ) BFGZCFGZABGZCAHIZCBHIZGZJZWGWEWFWJWGWEWFWJJZWGWEWEWLJZJWGWGWEWMWGWEKZWE
      WFWGWJWNWEWFWKAFGZWEWFWKWNWOWECLGZWKWFWOWEWPMWGCANZHIZWIOZWJWOWEWPWGWSJWO
      WEWGWPWSWOWEWGWPWSJZJWEWOWEWGWTWEWOKWQBOZWTWEWGKWEWQFGZXAWTJWOWEXBKXAWTWP
      WRCDPZHIZOZJWPWRWROZJWPWRCEPZHIZOZJWPWRCXGNZHIZOZJWTDEBWQXCWQQZXEXFWPXMXD
      WRWRXCWQCHRSTDEUAZXEXIWPXNXDXHWRXCXGCHRSTXCXJQZXEXLWPXOXDXKWRXCXJCHRSTXCB
      QZXEWSWPXPXDWIWRXCBCHRSTXFWPWRUBUCXGFGZXBKZWQXGOZKWPXIXLXRWPXIXLJZJZXSXQY
      AXBXQWPXTXGLGZWPXTXQYBWPKZXLWRXHNZOZXIYCXKYDWRWPYBXKYDQCXGUDUESXIXHYDOYEW
      RXHYDUFXHUGUHUIXGUJUKULUMUMUNUOULAUPUQBURZWGXAWEYFWGXAABUSUTBVAUKVBVCVDVE
      VFWOWPWSWJJZWEWPWOYGWPALGZYGWOWPYHKZWSWHNZWIOZWJYIWRYJWICAUDVGWHVHGYKWJJC
      AHVIWHWIVHVJVKVLAUJVMUEVNVOCUJVPABVQVRVSVTWAWBWCWDUT $.
      $([3-Feb-96]$)
  $}

   $( Weak ordering property of addition. $)
   nnaordw $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A (_ B -> ( C +_o A ) (_ ( C +_o B ) ) ) $=
     ( com wcel w3a wceq wo coa co wss nnaordi wi opreq2 a1i im2ord con wa wb
     nnont im2an 3adant3 onsselt syl anandi nnacl sylbi ancoms 3impa 3imtr4d )
     ADEZBDEZCDEZFZABEZABGZHZCAIJZCBIJZEZURUSGZHZABKZURUSKZUNUOUTUPVAABCLUPVAMU
     NABCINOPUNAQEZBQEZRZVCUQSUKULVGUMUKVEULVFATBTUAUBABUCUDUNURQEZUSQEZRZVDVBS
     UKULUMVJUMUKULRZVJUMVKRUMUKRZUMULRZRVJUMUKULUEVLVHVMVIVLURDEVHCAUFURTUDVMU
     SDEVICBUFUSTUDUAUGUHUIURUSUCUDUJ $.
     $([17-Sep-95]$)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $( ` x y ` are dummy. $)
    $( Ordering property of multiplication.  Half of Proposition 8.19 of
       Takeuti and Zaring, p. 63, limited to natural numbers. $)
    nnmordi $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A e. B /\ (/) e. C ) -> ( C ._o A ) e. ( C ._o B ) ) ) $=
      ( vx vy com wcel w3a c0 comu co csuc wss wi wa cv wceq opreq2 sseq2d
      bilimd weq ssid a1i coa sstr con pm3.27 nnont oa0 3syl sseq1d nnaordw
      nullss mpi peano1 mp3an1 mpbid nnmcl sylan5 anabss5 syl5 comm nnmsuc
      sylibrd exp adantr a2d findsg peano2b sylan5b word ordsucssi imp nnord
      sylan exp4b pm2.43b com34 3imp ssel syl6bi nnaordi eleq1d sylibd syl5d
      ancoms 3adant2 syld imp3a ) AFGZBFGZCFGZHZABGZICGZCAJKZCBJKZGZWMWNCALZJKZ
      WQMZWOWRNZWJWKWLWNXANWJWKWNWLXAWJWKWNWLXANZNWKWJWKWNXCWKWJOWSBMZXCWKWNOWK
      WSFGZXDXCNWJWKXEOXDXCWLWTCDPZJKZMZNWLWTWTMZNWLWTCEPZJKZMZNWLWTCXJLZJKZMZN
      XCDEBWSXFWSQZXHXIWLXPXGWTWTXFWSCJRSTDEUAZXHXLWLXQXGXKWTXFXJCJRSTXFXMQZXHX
      OWLXRXGXNWTXFXMCJRSTXFBQZXHXAWLXSXGWQWTXFBCJRSTXIWLWTUBUCXJFGZXEOZWSXJMZO
      WLXLXOYAWLXLXONZNZYBXTYDXEWLXTYCWLXTYCWLXTOZXLWTXKCUDKZMZXOXLYEYGXLXKYFMZ
      YGYEWTXKYFUEWLXTYHWLXKFGZYHYEWLYIOZXKIUDKZYFMZYHYJYKXKYFYJYIXKUFGYKXKQWLY
      IUGXKUHXKUIUJUKIFGZWLYIYLYMWLYIHICMYLICXKULCUMUNUOUPUQCXJURUSUTVAVBYEXNYF
      WTCXJVCSVDVEVBVFVFVGVHVEAVIVJBVKZWNXDWKYNWNXDABVLVMBVNVOVAVPVQVRVSWJWLXAX
      BNZWKWLWJYOWLWJOZXAWPWPCUDKZGZWRWOYPXAYQWQMYRWRNYPWTYQWQCAVCUKYQWQWPVTWAW
      LWJWOYRNZWLWPFGZYSYPWLYTOZWOWPIUDKZYQGZYRYMWLYTWOUVCNICWPWBUOUPUVAUVBWPYQ
      UVAYTWPUFGUVBWPQWLYTUGWPUHWPUIUJWCWDCAURUSUTWEWFWGWHWI $.
      $([18-Sep-95]$)
  $}

  $( Ordering property of multiplication.  Proposition 8.19 of
     Takeuti and Zaring, p. 63, limited to natural numbers. $)
  nnmord $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
               ( ( A e. B /\ (/) e. C ) <-> ( C ._o A ) e. ( C ._o B ) ) ) $=
    ( com wcel w3a c0 wa comu co nnmordi wi wceq wo wn opreq2 a1i exp3a com23
    3com12 imp im2ord pm2.16d word wb pm3.26 df-3an ancom anandi 3bitr sylib
    nnmcl nnord syl im2an ordtri 3syl 3simpa 3imtr4d exp ancr syl6 opreq1
    cleq1d nnm0r syl5bir comm noel2 nsyli adantr ord0eln0 adantl sylibrd
    3adant1 syl5d pm2.43d impbid ) ADEZBDEZCDEZFZABEZGCEZHZCAIJZCBIJZEZABCKWAWG
    WDWAWGWCWDWGWAWGWCWBLWCWDLWAWCWGWBWAWCWGWBLWAWCHZWEWFMZWFWEEZNZOZABMZBAEZNZ
    OZWGWBWHWOWKWHWMWIWNWJWMWILWHABCIPQWAWCWNWJLZVSVRVTWCWQLVSVRVTFZWNWCWJWRWNW
    CWJBACKRSTUAUBUCWHVTVRHZVTVSHZHZWEUDZWFUDZHWGWLUEWHWAXAWAWCUFZWAVRVSHZVTHVT
    XEHXAVRVSVTUGXEVTUHVTVRVSUIUJUKWSXBWTXCWSWEDEXBCAULWEUMUNWTWFDEXCCBULWFUMUN
    UOWEWFUPUQWHAUDZBUDZHZWBWPUEWHWAXEXHXDVRVSVTURVRXFVSXGAUMBUMUOUQABUPUNUSUTS
    WCWBVAVBVSVTWGWCLVRVSVTHWGCGMZOZWCVSWGXJLVTVSXIWFGMZWGXIVSXKXIXKGBIJZGMVSXI
    WFXLGCGBIVCVDBVEVFVGWFWEVHVIVJVTWCXJUEZVSVTCUDXMCUMCVKUNVLVMVNVOVPVQ $.
    $([22-Jan-96]$)

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Addition of natural numbers is associative.  (This can be extended to
       all ordinals with a longer proof.)  Theorem 4K(1) of Enderton, p. 81. $)
    nnaass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A +_o B ) +_o C ) = ( A +_o ( B +_o C ) ) ) $=
      ( vx vy com wcel coa co wceq wi wa cv c0 csuc opreq2 opreq2d cleq12d
      bilimd weq nnacl nna0 syl adantl eqtr4d nnasuc sylan anass sylan5 eqtrd
      sylbi suceq syl5bir exp comm a2d finds 3imp ) AFGZBFGZCFGZABHIZCHIZABCHIZ
      HIZJZUSUTVAVFKVAUSUTLZVFVGVBDMZHIZABVHHIZHIZJZKVGVBNHIZABNHIZHIZJZKVGVBEM
      ZHIZABVQHIZHIZJZKVGVBVQOZHIZABWBHIZHIZJZKVGVFKDECVHNJZVLVPVGWGVIVMVKVOVHN
      VBHPWGVJVNAHVHNBHPQRSDETZVLWAVGWHVIVRVKVTVHVQVBHPWHVJVSAHVHVQBHPQRSVHWBJZ
      VLWFVGWIVIWCVKWEVHWBVBHPWIVJWDAHVHWBBHPQRSVHCJZVLVFVGWJVIVCVKVEVHCVBHPWJV
      JVDAHVHCBHPQRSVGVMVBVOVGVBFGZVMVBJABUAZVBUBUCUTVOVBJUSUTVNBAHBUBQUDUEVQFG
      ZVGWAWFVGWMWAWFKZVGWMWNVGWMLZWFVROZVTOZJWAWOWCWPWEWQWKWMWCWPJVGVBVQUFWLUG
      WOUSUTWMLZLZWEWQJUSUTWMUHWSWEAVSOZHIZWQWRWEXAJUSWRWDWTAHBVQUFQUDUSVSFGXAW
      QJWRAVSUFBVQUAUIUJUKRVRVTULUMUNUOUPUQUOUNUR $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Distributive law for natural numbers.  (This can be extended to
       all ordinals with a longer proof.)  Theorem 4K(3) of Enderton, p. 81. $)
    nndist $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( A ._o ( B +_o C ) ) = ( ( A ._o B ) +_o ( A ._o C ) ) ) $=
      ( vx vy com wcel coa co comu wceq wa cv wi c0 csuc opreq2 opreq2d
      cleq12d bilimd weq nnmcl nna0 syl con pm3.26 nnont om0 3syl adantl
      3eqtr4rd w3a nnasuc 3adant1 nnmsuc nnacl sylan5 3impb eqtrd 3adant2
      nnaass 3sylan1 3sylan2 3exp exp4b pm2.43a com4r pm2.43i 3imp eqtr4d
      opreq1 syl5bir com3r imp3a a2d finds exp3a com3l ) AFGZBFGZCFGZABCHIZJIZA
      BJIZACJIZHIZKZWAVSVTWGWAVSVTWGVSVTLZABDMZHIZJIZWDAWIJIZHIZKZNWHABOHIZJIZW
      DAOJIZHIZKZNWHABEMZHIZJIZWDAWTJIZHIZKZNWHABWTPZHIZJIZWDAXFJIZHIZKZNWHWGND
      ECWIOKZWNWSWHXLWKWPWMWRXLWJWOAJWIOBHQRXLWLWQWDHWIOAJQRSTDEUAZWNXEWHXMWKXB
      WMXDXMWJXAAJWIWTBHQRXMWLXCWDHWIWTAJQRSTWIXFKZWNXKWHXNWKXHWMXJXNWJXGAJWIXF
      BHQRXNWLXIWDHWIXFAJQRSTWICKZWNWGWHXOWKWCWMWFXOWJWBAJWICBHQRXOWLWEWDHWICAJ
      QRSTWHWDOHIZWDWRWPWHWDFGZXPWDKABUBZWDUCUDWHWQOWDHWHVSAUEGWQOKVSVTUFAUGAUH
      UIRWHWOBAJVTWOBKVSBUCUJRUKWTFGZWHXEXKXSVSVTXEXKNZVSVTXSXTVSVTXSXTVSVTXSUL
      ZXKXBAHIZXDAHIZKXEYAXHYBXJYCYAXHAXAPZJIZYBYAXGYDAJVTXSXGYDKVSBWTUMUNRVSVT
      XSYEYBKZVSXAFGYFVTXSLAXAUOBWTUPUQURUSYAXJWDXCAHIZHIZYCYAXIYGWDHVSXSXIYGKV
      TAWTUOUTRVSVTXSYCYHKZVSVTXSYINNVSVTXSVSYIVTVSXSVSYINZNVSVTVSXSYJWHVSXSLZV
      SYIWHXCFGZVSYIYKXQYLVSYIWHWDXCAVAXRVBAWTUBVCVDVEVFVGVHVIVJSXBXDAHVKVLVDVM
      VNVOVPVQVRVI $.
      $([20-Sep-95]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $( ` x y ` are dummy. $)
    $( Multiplication of natural numbers is associative.  (This can be extended
       to all ordinals with a longer proof.)  Theorem 4K(4) of Enderton,
       p. 81. $)
    nnmass $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                 ( ( A ._o B ) ._o C ) = ( A ._o ( B ._o C ) ) ) $=
      ( vx vy com wcel comu co wceq wa cv wi c0 csuc opreq2 opreq2d cleq12d
      bilimd weq nnmcl nnm0 syl sylan9eqr eqtr4d w3a coa nnmsuc sylan 3impa
      3adant1 nndist 3sylan2 3exp exp3a com34 pm2.43d 3imp eqtrd opreq1
      syl5bir com3r imp3a a2d finds com3l ) AFGZBFGZCFGZABHIZCHIZABCHIZHIZJZVIV
      GVHVNVIVGVHVNVGVHKZVJDLZHIZABVPHIZHIZJZMVOVJNHIZABNHIZHIZJZMVOVJELZHIZABW
      EHIZHIZJZMVOVJWEOZHIZABWJHIZHIZJZMVOVNMDECVPNJZVTWDVOWOVQWAVSWCVPNVJHPWOV
      RWBAHVPNBHPQRSDETZVTWIVOWPVQWFVSWHVPWEVJHPWPVRWGAHVPWEBHPQRSVPWJJZVTWNVOW
      QVQWKVSWMVPWJVJHPWQVRWLAHVPWJBHPQRSVPCJZVTVNVOWRVQVKVSVMVPCVJHPWRVRVLAHVP
      CBHPQRSVOWANWCVOVJFGZWANJABUAZVJUBUCVHVGWCANHINVHWBNAHBUBQAUBUDUEWEFGZVOW
      IWNXAVGVHWIWNMZVGVHXAXBVGVHXAXBVGVHXAUFZWNWFVJUGIZWHVJUGIZJWIXCWKXDWMXEVG
      VHXAWKXDJZWSXAXFVOVJWEUHWTUIUJXCWMAWGBUGIZHIZXEXCWLXGAHVHXAWLXGJVGBWEUHUK
      QVGVHXAXHXEJZVGVHXAXIMVGVHXAVHXIVGVHXAVHXIMVGVHXAKZVHXIVGWGFGVHXIXJAWGBUL
      BWEUAUMUNUOUPUQURUSRWFWHVJUGUTVAUNVBVCVDVEUOVFUR $.
      $([20-Sep-95]$)
  $}

  ${
    $d x A $.  $d x y z B $.  $( ` x y z` are dummy. $)
    $( Addition of natural numbers is commutative.  Theorem 4K(2) of
       Enderton, p. 81. $)
    nnacom $p |- ( ( A e. om /\ B e. om ) -> ( A +_o B ) = ( B +_o A ) ) $=
      ( vx vy vz com wcel coa co wceq cv wi c0 csuc opreq1 opreq2 cleq12d
      bilimd weq nna0r nna0 eqtr4d wa suceq syl peano2b sylbi con oasuc nnont
      tz7.24 syl2an im2an 3syl syl5bir exp comm a2d finds imp nnasuc ) AFGBFGZA
      BHIZBAHIZJZVBCKZBHIZBVFHIZJZLVBMBHIZBMHIZJZLVBDKZBHIZBVMHIZJZLVBVMNZBHIZB
      VQHIZJZLVBVELCDAVFMJZVIVLVBWAVGVJVHVKVFMBHOVFMBHPQRCDSZVIVPVBWBVGVNVHVOVF
      VMBHOVFVMBHPQRVFVQJZVIVTVBWCVGVRVHVSVFVQBHOVFVQBHPQRVFAJZVIVEVBWDVGVCVHVD
      VFABHOVFABHPQRVBVJBVKBTBUAUBVMFGZVBVPVTVBWEVPVTLZVBWEWFVBWEUCZVTVNNZVONZJ
      VPWGVRWHVSWIVBWEVRWHJZWEVQVFHIZVMVFHIZNZJZLWEVQMHIZVMMHIZNZJZLWEVQEKZHIZV
      MWSHIZNZJZLWEVQWSNZHIZVMXDHIZNZJZLWEWJLCEBWAWNWRWEWAWKWOWMWQVFMVQHPWAWLWP
      JWMWQJVFMVMHPWLWPUDUEQRCESZWNXCWEXIWKWTWMXBVFWSVQHPXIWLXAJWMXBJVFWSVMHPWL
      XAUDUEQRVFXDJZWNXHWEXJWKXEWMXGVFXDVQHPXJWLXFJWMXGJVFXDVMHPWLXFUDUEQRVFBJZ
      WNWJWEXKWKVRWMWHVFBVQHPXKWLVNJWMWHJVFBVMHPWLVNUDUEQRWEWOVQWQWEVQFGWOVQJVM
      UFVQUAUGWEWPVMJWQVQJVMUAWPVMUDUEUBWSFGZWEXCXHWEXLXCXHLZWEXLXMWEXLUCZXHWTN
      ZXBNZJXCXNXEXOXGXPVQUHGZWSUHGZXEXOJWEXLVQWSUIWEVMUHGZXQVMUJZVMUKUEWSUJZUL
      XNXSXRUCXFXBJXGXPJWEXSXLXRXTYAUMVMWSUIXFXBUDUNQWTXBUDUOUPUQURUSUTBVMVAQVN
      VOUDUOUPUQURUSUT $.
      $([6-May-95]$)
  $}

  ${
    $d x y A $.  $d x B $.  $( ` x y ` are dummy. $)
    $( Multiplication with successor.  Enderton, p. 82. $)
    nnmsucr $p |- ( ( A e. om /\ B e. om ) ->
                  ( suc A ._o B ) = ( ( A ._o B ) +_o B ) ) $=
      ( vx vy com wcel csuc comu co coa wceq cv wi c0 opreq2 id opreq12d
      cleq12d bilimd weq peano2b nnm0 sylbi opreq1d peano1 nna0 ax-mp syl6eq
      eqtr4d wa nnmsuc sylanb nnacom suceq syl nnasuc ancoms 3eqtr4d opreq2d
      nnaass 3sylan3b nnmcl 3sylan1 3expb anabss an41r3s eqtrd opreq1 syl5bir
      exp a2d finds imp ) BEFZAEFZAGZBHIZABHIZBJIZKZVNVOVTVOVPCLZHIZAWAHIZWAJIZ
      KZMVOVPNHIZANHIZNJIZKZMVOVPDLZHIZAWJHIZWJJIZKZMVOVPWJGZHIZAWOHIZWOJIZKZMV
      OVTMCDBWANKZWEWIVOWTWBWFWDWHWANVPHOWTWCWGWANJWANAHOWTPQRSCDTZWEWNVOXAWBWK
      WDWMWAWJVPHOXAWCWLWAWJJWAWJAHOXAPQRSWAWOKZWEWSVOXBWBWPWDWRWAWOVPHOXBWCWQW
      AWOJWAWOAHOXBPQRSWABKZWEVTVOXCWBVQWDVSWABVPHOXCWCVRWABJWABAHOXCPQRSVOWFNW
      HVOVPEFZWFNKAUAZVPUBUCVOWHNNJIZNVOWGNNJAUBUDNEFXFNKUENUFUGUHUIWJEFZVOWNWS
      XGVOWNWSMZVOXGXHVOXGUJZWSWKVPJIZWMVPJIZKWNXIWPXJWRXKXDXGWPXJKVOVPWJUKXEUL
      XIWRWLAJIZWOJIZXKXIWQXLWOJAWJUKUDXIWLAWOJIZJIZWLWJVPJIZJIZXMXKXIXNXPWLJXI
      AWJJIZGZWJAJIZGZXNXPXIXRXTKXSYAKAWJUMXRXTUNUOAWJUPXGVOXPYAKWJAUPUQURUSXIX
      MXOKZXIVOXGYBWLEFZVOXGYBXIYCVOWOEFYBXGWLAWOUTWJUAVAAWJVBZVCVDVEXIXKXQKZVO
      XGXGVOYEXIXGVOYEYCXGVOYEXIYCXGXDYEVOWLWJVPUTXEVAYDVCVDVFVEURVGRWKWMVPJVHV
      IUQVJVKVLVMUQ $.
      $([21-Sep-95]$)
  $}

  ${
    $d x A $.  $d x y B $.  $( ` x y ` are dummy. $)
    $( Multiplication of natural numbers is commutative.  Theorem 4K(5) of
       Enderton, p. 81. $)
    nnmcom $p |- ( ( A e. om /\ B e. om ) -> ( A ._o B ) = ( B ._o A ) ) $=
      ( vx vy com wcel comu co wceq cv wi c0 csuc opreq1 opreq2 cleq12d bilimd
      weq nnm0r nnm0 eqtr4d wa coa nnmsucr nnmsuc ancoms syl5bir exp a2d finds
      imp ) AEFBEFZABGHZBAGHZIZULCJZBGHZBUPGHZIZKULLBGHZBLGHZIZKULDJZBGHZBVCGHZ
      IZKULVCMZBGHZBVGGHZIZKULUOKCDAUPLIZUSVBULVKUQUTURVAUPLBGNUPLBGOPQCDRZUSVF
      ULVLUQVDURVEUPVCBGNUPVCBGOPQUPVGIZUSVJULVMUQVHURVIUPVGBGNUPVGBGOPQUPAIZUS
      UOULVNUQUMURUNUPABGNUPABGOPQULUTLVABSBTUAVCEFZULVFVJVOULVFVJKVOULUBZVJVDB
      UCHZVEBUCHZIVFVPVHVQVIVRVCBUDULVOVIVRIBVCUEUFPVDVEBUCNUGUHUIUJUK $.
      $([21-Sep-95]$)
  $}

  $( Cancellation law for addition of natural numbers. $)
  nnacan $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
                   ( ( A +_o B ) = ( A +_o C ) -> B = C ) ) $=
    ( com wcel w3a coa co wo wn wceq wi nnaordi 3comr 3com13 im2ord pm2.16d wb
    word wa ordtri2 nnacl nnord syl syl2an 3impdi 3adant1 3imtr4d ) ADEZBDEZCDE
    ZFZABGHZACGHZEZUNUMEZIZJZBCEZCBEZIZJZUMUNKZBCKZULVAUQULUSUOUTUPUJUKUIUSUOLB
    CAMNUKUJUIUTUPLCBAMOPQUIUJUKVCURRZUMSZUNSZVEUIUJTZUIUKTZUMUNUAVHUMDEVFABUBU
    MUCUDVIUNDEVGACUBUNUCUDUEUFUJUKVDVBRZUIBSCSVJUJUKBCUABUCCUCUEUGUH $.
    $([27-Oct-95]$)

  $( Cancellation law for multiplication of natural numbers. $)
  nnmcan $p |- ( ( ( A e. om /\ B e. om /\ C e. om ) /\ (/) e. A ) ->
                   ( ( A ._o B ) = ( A ._o C ) -> B = C ) ) $=
    ( com wcel w3a c0 wa comu co wo wn wceq wi nnmordi 3comr 3com13 im2ord
    andir syl5ib exp3a com23 imp pm2.16d wb word ordtri2 nnmcl nnord syl
    syl2an 3impdi adantr 3adant1 3imtr4d ) ADEZBDEZCDEZFZGAEZHZABIJZACIJZEZVCVB
    EZKZLZBCEZCBEZKZLZVBVCMZBCMZVAVJVFUSUTVJVFNUSVJUTVFUSVJUTVFUSVHUTHZVIUTHZKV
    FVJUTHUSVNVDVOVEUQURUPVNVDNBCAOPURUQUPVOVENCBAOQRVHVIUTSTUAUBUCUDUSVLVGUEZU
    TUPUQURVPVBUFZVCUFZVPUPUQHZUPURHZVBVCUGVSVBDEVQABUHVBUIUJVTVCDEVRACUHVCUIUJ
    UKULUMUSVMVKUEZUTUQURWAUPBUFCUFWAUQURBCUGBUICUIUKUNUMUO $.
    $([26-Oct-95]$)

  ${
    $d x y z A $.  $d x y z B $.  $( ` y z ` are dummy. $)
    nnordexlem.1 $e |- A e. om $.
    nnordexlem.2 $e |- B e. om $.
    $( Equivalence for ordering.  Compare Exercise 23, p. 88 of Enderton. $)
    nnordexlem   $p |- ( A e. B <->
                       E. x ( ( x e. om /\ (/) e. x ) /\ ( A +_o x ) = B ) ) $=
      ( vy vz wcel cv com c0 wa coa co wceq wex wss wel wn wi wal nnaordi
      mp3an1 mpan nnacom mpan2 eleq1d sylibd opreq1 nna0r ax-mp syl6eq eleq2d
      biimpcd pm2.16d syl6 word wb nnacl nnord ordssnel 3syl biimpd syl5d
      com23 imp adantr ord0eln0 syl pm5.32i biimpr exp csuc nnsuc ax-17 hba1
      hbim eleq1 peano2b syl6bbr con nnont onsssuct nnon adantl opreq2 nnasuc
      sylan9eq bitr4d biland birimd biimpa exp4b pm2.03 syl8 com4t eleq2
      visset sucid mpbiri syl5 comm pm2.43i sylbi a4sd 19.23ai pm2.43a wo
      ordssel ord cleqcom syl6ib syl6d jcad syld 19.22dv cab cin weq sseq2d
      bi2and abcl disj birim bial bitr bi2an biex tz7.5 ordom ssab noel3
      elisseti cla4ev w3a nnaordw nullss mpi peano1 mp3an nna0 mp2an 3sstr3
      mpbir mpbi eleq1i syl6bb syl5bi 19.23aiv impbi ) BCHZAIZJHZKUVLHZLZBUVLMN
      ZCOZLZAPZUVKUVMCUVPQZLZFIZJHZCBUWBMNZQZLZFARZSZTZFUAZLZAPZUVSUVKUWKUVRAUW
      KUVKUVRUWKUVKUVLKOZSZUVRUWAUVKUWNTZUWJUVMUVTUWOUVMUVKUVTUWNUVMUVKUVPCHZSZ
      UWNUVTUVMUVKUVPUVLCMNZHZUWQUWNTUVMUVKUVLBMNZUWRHZUWSCJHZUVMUVKUXATZBJHZUX
      BUVMUXCBCUVLUBDUCEUDUVMUWTUVPUWRUVMUXDUWTUVPOUVLBUEDUFUGUHUWSUWMUWPUWMUWS
      UWPUWMUWRCUVPUWMUWRKCMNZCUVLKCMUIUXBUXECOECUJUKULUMUNUOUPUVMUVTUWQUVMUVPJ
      HZUVPUQZUVTUWQURZUXDUVMUXFBUVLUSDUDZUVPUTZCUQZUXGUXHCUVPVAUXBUXKECUTUKZUD
      VBVCVDVEVFVGUWKUWNUVOUVQUWAUWNUVOTZUWJUVMUXMUVTUVMUWNUVOUVOUVMUWNLUVMUVNU
      WNUVMUVLUQUVNUWNURUVLUTUVLVHVIVJVKVLVGVGUWAUWJUWNUVQTUWAUWNUWJUVQUWAUWNUW
      JCUVPHZSZUVQUVMUWNUWJUXOTZTUVTUWNUVMUXPUVMUWNUVLUWBVMZOZFPZUVMUXPTZUVMUWN
      UXSFUVLVNVLUXRUXTFUVMUXPFUVMFVOUWJUXOFUWIFVPUXOFVOVQVQUXRUVMUXPUXRUVMLZUW
      IUXOFUYAUXRUWCLZUWIUXOTZUXRUVMUWCUXRUVMUXQJHUWCUVLUXQJVRUWBVSVTVJUYBUYCUX
      RUWCUYBUYCTZUWCUXRUYDUWCUWGUYDUXRUYBUWIUWCUWGUXOUYBUWIUWCUXNUWHTUWGUXOTUY
      BUWIUWCUXNUWHUYBUWIUWCUXNLZUWHTUYBUWFUYEUWHUYBUWEUXNUWCUYBUWECUWDVMZHZUXN
      UWCUWEUYGURZUXRUWCUWDJHZUWDWAHZUYHUXDUWCUYIBUWBUSDUDUWDWBCWAHUYJUYHCUWDWC
      CEWDUDVBWEUYBUVPUYFCUXRUWCUVPBUXQMNZUYFUVLUXQBMWFUXDUWCUYKUYFOBUWBWGDUDWH
      UMWIWJWKWLWMUXNUWGWNWOWPUXRUWGUWBUXQHUVLUXQUWBWQUWBFWRZWSWTXAXBVFXCXDXEVL
      XFUPXGVGUWAUXOCUVPOZUVQUWAUXNUYMUVMUVTUXNUYMXHZUVMUXFUXGUVTUYNURZUXIUXJUX
      KUXGUYOCUVPXIUXLUDVBWLXJCUVPXKXLXMVEVFXNXOXBXPUVLGIZJHZCBUYPMNZQZLZGXQZHZ
      VUAUVLXRKOZLZAPZUWLVUDUWKAVUBUWAVUCUWJUYTUWAGUVLAWRGAXSZUYQUVMUYSUVTUYPUV
      LJVRVUFUYRUVPCUYPUVLBMWFXTYAYBVUCUWBVUAHZUWHTZFUAUWJFVUAUVLYCVUHUWIFVUGUW
      FUWHUYTUWFGUWBUYLGFXSZUYQUWCUYSUWEUYPUWBJVRVUIUYRUWDCUYPUWBBMWFXTYAYBYDYE
      YFYGYHVUAJQZVUAKOSZVUEJUQVUJVUKLVUEAJVUAYIYJUDUYSGJYKVUKUYTGPZUYTGYLUXBCB
      CMNZQZVULUYTUXBVUNLGCCJEYMUYPCOZUYQUXBUYSVUNUYPCJVRVUOUYRVUMCUYPCBMWFXTYA
      YNECKMNZCBMNZCVUMKJHZUXDUXBVUPVUQQZVURUXDUXBYOKBQVUSKBCYPBYQYRYSDEYTUXBVU
      PCOECUVAUKUXBUXDVUQVUMOCBUEEDUVBUVCUVBUVDUVBUVEYRUVRUVKAUVOUVQUVKUVQUVOUV
      KUVQBKMNZUVPHZUVKUVOUVQVVAVUTCHUVKUVPCVUTWQVUTBCUXDVUTBODBUVAUKUVFUVGUVMU
      VNVVAUVMUXDUVNVVATZVURUVMUXDVVBKUVLBUBYSUCDUFVFUVHXBVFUVIUVJ $.
      $([12-Jan-96]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( Equivalence for ordering.  Compare Exercise 23, p. 88 of Enderton. $)
    nnordex $p |- ( ( A e. om /\ B e. om ) -> ( A e. B <->
                  E. x ( ( x e. om /\ (/) e. x ) /\ ( A +_o x ) = B ) ) ) $=
      ( com wcel cv c0 wa coa co wceq wex wb cded eleq1 opreq1 cleq1d biland
      biexdv bi2bid eleq2 cleq2 peano1 elimel nnordexlem dedth2h ) BDEZCDEZBCEZ
      AFZDEGUJEHZBUJIJZCKZHZALZMUGBGNZCEZUKUPUJIJZCKZHZALZMUPUHCGNZEZUKURVBKZHZ
      ALZMBCGGBUPKZUIUQUOVABUPCOVGUNUTAVGUMUSUKVGULURCBUPUJIPQRSTCVBKZUQVCVAVFC
      VBUPUAVHUTVEAVHUSVDUKCVBURUBRSTAUPVBBGDUCUDCGDUCUDUEUF $.
      $([5-Dec-95]$)
  $}

  $( One is a natural number. $)
  1nn $p |- 1_o e. om $=
    ( c1o c0 csuc com df-1o peano1 peano2 eqeltr ) ABCDEBFGH $.
    $([29-Oct-95]$)


