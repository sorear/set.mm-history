$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol. $)
  $c om $. $( Greek letter omega $)

  $( Extend class notation to include the class of natural numbers. $)
  com $a class om $.

  ${
    $d x y $.
    $( Define the class of natural numbers.  Compare Bell and Machover, p. 471.
       Later, we will show that if we assume the Axiom of Infinity, ` om `
       is a set, and ` om ` can then be defined per ~ dfom2 . $)
    df-om $a |- om = { x | ( Ord x /\ A. y ( Lim y -> x e. y ) ) } $.
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    elom.1 $e |- A e. V $.
    $( Membership in omega.  The hypothesis could be eliminated were we to
       assume the Axiom of Infinity.  $)
    elom $p |- ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) $=
      ( vy cv word wlim wel wi wal wa wcel com wceq ordeq eleq1 bilimd bialdv
      bi2and df-om vtoclab ) DEZFZAEZGZDAHZIZAJZKBFZUEBUDLZIZAJZKDBMCUBBNZUCUIU
      HULUBBOUMUGUKAUMUFUJUEUBBUDPQRSDATUA $.
      $([15-May-94]$)
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    $( Membership in omega.  The antecedent could be eliminated were we to
       assume the Axiom of Infinity.  $)
    elomg $p |- ( A e. B ->
                ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) ) $=
      ( vy cv com wcel word wlim wel wi wal wa eleq1 wceq ordeq bilimd bialdv
      bi2and visset elom vtoclgb ) DEZFGUCHZAEZIZDAJZKZALZMBFGBHZUFBUEGZKZALZMD
      BCUCBFNUCBOZUDUJUIUMUCBPUNUHULAUNUGUKUFUCBUENQRSAUCDTUAUB $.
      $([25-Mar-95]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Omega is a subset of ` On `. $)
    omsson $p |- om (_ On $=
      ( vx vy com con cv wcel word wlim wel wi wal visset elom pm3.26bd elon
      sylibr ssriv ) ACDAEZCFZRGZRDFSTBEHABIJBKBRALZMNRUAOPQ $.
      $([13-Jun-94]$)
  $}

  $( A natural number is an ordinal. $)
  nnont $p |- ( A e. om -> A e. On ) $=
    ( com con omsson ssi ) BCADE $.
    $([27-Jun-94]$)

  ${
    nnon.1 $e |- A e. om $.
    $( A natural number is an ordinal. $)
    nnon   $p |- A e. On $=
      ( com wcel con nnont ax-mp ) ACDAEDBAFG $.
      $([27-Jun-94]$)
  $}

  $( A natural number is ordinal. $)
  nnord $p |- ( A e. om -> Ord A ) $=
    ( com wcel con word nnont eloni syl ) ABCADCAEAFAGH $.
    $([17-Oct-95]$)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( Omega is ordinal.  Theorem 7.32 of Takeuti and Zaring, p. 43. $)
    ordom $p |- Ord om $=
      ( vy vx vz com wtr con wss word wel cv wcel wa wi wal dftr2 wlim tz7.6
      nnord sylan ancoms trel exp3a comm limord ordtr syl syl5 a2d 19.20dv
      visset elom pm3.27bd imp jca sylibr ax-gen mpgbir omsson tz7.12 trssord
      mp3an ) DEZDFGFHDHVBABIZBJZDKZLZAJZDKZMZBNAABDOVIBVFVGHZCJZPZACIZMZCNZLVH
      VFVJVOVEVCVJVDHZVCVJVEVDVGQVDRSTVCVEVOVCVLBCIZMZCNZVOVEVCVRVNCVCVLVQVMVCV
      KEZVQVMMZVLVTVCWAVTVCVQVMVKVGVDUAUBUCVLVKHVTVKUDVKUEUFUGUHUIVEVPVSCVDBUJU
      KULUGUMUNCVGAUJUKUOUPUQURUSDFUTVA $.
      $([18-Oct-95]$)
  $}

  $( A member of a natual number is a natural number. $)
  elnn $p |- ( ( A e. B /\ B e. om ) -> A e. om ) $=
    ( com wtr wcel wa wi word ordom ordtr ax-mp trel ) CDZABEBCEFACEGCHMICJKCAB
    LK $.
    $([21-Jun-98]$)

  $( Omega is either an ordinal number or the set of all ordinals (not
     presupposing the Axiom of Infinity).  Remark of Takeuti and Zaring,
     p. 43. $)
  omon $p |- ( om e. On \/ om = On ) $=
    ( com word con wcel wceq wo ordom tz7.14 ax-mp ) ABACDACEFGAHI $.
    $([10-May-98]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
  $( A natural number is not a limit ordinal. $)
  nnlim $p |- ( A e. om -> -. Lim A ) $=
    ( vx com wcel wlim word wn nnord ordeirr syl cv wi wal wa elomg biimpd
    pm2.43i pm3.27d wceq limeq eleq2 bi2imd cla4gv mpd mtod ) ACDZAEZAADZUFAFZU
    HGAHAIJUFBKZEZAUJDZLZBMZUGUHLZUFUIUNUFUIUNNZUFUFUPBACOPQRUMUOBACUJASUKUGULU
    HUJATUJAAUAUBUCUDUE $.
    $([18-Oct-95]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( A non-zero natural number is a successor. $)
    nnsuc $p |- ( ( A e. om /\ -. A = (/) ) -> E. x A = suc x ) $=
      ( com wcel c0 wceq wn wa cv csuc wex wlim nnlim adantr word wi cuni wb
      orduninsuc w3a df-lim biimpr 3exp imp sylbird nnord sylan mt3d ) BCDZBEFG
      ZHBAIJFAKZBLZUIULGUJBMNBOZUJUKGZULPUIUMUJHUNBBQFZULUMUOUNRUJABSNUMUJUOULP
      UMUJUOULULUMUJUOTBUAUBUCUDUEBUFUGUH $.
      $([18-Oct-95]$)
  $}

  $( Omega is a limit ordinal.  Theorem 2.8 of Bell and Machover, p. 473.
     Our proof does not require the axiom of Infinity. $)
  limom $p |- Lim om $=
    ( vx com word wlim ordom con wcel wceq wo tz7.14 wn ordeirr ax-mp cv wi
    wal wa elomg wss wb ordssnel adantr ordssel biimpd nnlim a1i limeq pm2.16d
    comm jaod sylan9 sylbird a3d mpan12 limord sylan exp pm2.43b 19.21aiv
    jctil syl5bir mt3i limon mpbiri jaoi syl ) BCZBDZEVGBFGZBFHZIVHBJVIVHVJVIVH
    BBGZVGVKKEBLMVIVKVGANZDZBVLGZOZAPZQVHKZABFRVQVPVGVQVOAVQVMVNVMVQVOVLCZVQVOV
    MVRVGVQVOEVRVGQZVQQZVNVMVTVNKZVLBSZVMKZVSWBWATVQVLBUAUBVSWBVLBGZVLBHZIZVQWC
    VSWBWFVLBUCUDVQWDWCWEWDWCOVQVLUEUFWEVQWCWEVMVHWEVMVHVLBUGUDUHUIUJUKULUMUNVL
    UOUPUQURUSEUTVAVBVJVHFDVCBFUGVDVEVFM $.
    $([26-Mar-95]$)

  $( ` x ` is dummy. $)
  $( One of Peano's axioms for arithmetic.  Proposition 7.30(1) of Takeuti
     and Zaring, p. 42. $)
  peano1 $p |- (/) e. om $=
    ( vx c0 com wcel word cv wlim wi wal wa ord0 0ellim ax-gen pm3.2i zfnull
    elom mpbir ) BCDBEZAFZGBSDHZAIZJRUAKTASLMNABOPQ $.
    $([15-May-94]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    peano2.1 $e |- A e. om $.
    $( One of Peano's axioms for arithmetic.  Proposition 7.30(2) of Takeuti
       and Zaring,  p. 42. $)
    peano2 $p |- suc A e. om $=
      ( vx csuc com wcel word cv wlim wi wal wa nnon onsuc onord elisseti elom
      mpbi pm3.27i a4i limsuc mpian ax-gen pm3.2i sucex mpbir ) ADZEFUGGZCHZIZU
      GUIFZJZCKZLUHUMUGAABMZNOULCUJAUIFZUKUJUOJZCAGZUPCKZAEFUQURLBCAAEBPZQRSTCA
      UNUAUBUCUDCUGAUSUEQUF $.
      $([15-Jun-94]$)
  $}

  ${
    peano3.1 $e |- A e. om $.
    $( One of Peano's axioms for arithmetic.  Proposition 7.30(3) of Takeuti
       and Zaring, p. 42. $)
    peano3 $p |- -. suc A = (/) $=
      ( nsuceq0 ) AC $.
      $([15-Jun-94]$)
  $}

  ${
    peano4.1 $e |- A e. om $.
    peano4.2 $e |- B e. om $.
    $( One of Peano's axioms for arithmetic.  Proposition 7.30(4) of Takeuti
       and Zaring, p. 43. $)
    peano4 $p |- ( suc A = suc B <-> A = B ) $=
      ( csuc wceq wcel wn wo wi wa nnon onn2lp ianor mpbi wss eqimss onsuc
      onsucss sylibr onsuci sylib ord comm cleqcom bilor 3imtr4 jaoi ax-mp
      suceq impbi ) AEZBEZFZABFZABGZHZBAGZHZIZUNUOJZUPURKHUTABACLZMUPURNOUQVAUS
      UNUQUOUNUPUOUNAUMGZUPUOIUNULUMPVCULUMQAUMVBBBDLZRSTABVBUAUBUCUDUNUSUOUNUR
      UOUMULFZURBAFZIZUNURUOIVEBULGZVGVEUMULPVHUMULQBULVDAVBRSTBAVDUAUBULUMUEUO
      VFURABUEUFUGUCUDUHUIABUJUK $.
      $([16-Jun-94]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( One of Peano's axioms for arithmetic.  Proposition 7.30(5) of Takeuti
       and Zaring, p. 43. $)
    peano5 $p |- ( ( (/) e. A /\
                 A. x ( x e. om -> ( x e. A -> suc x e. A ) ) ) ->
                 om (_ A ) $=
      ( vy c0 wcel cv com csuc wi wal wa cdif wceq wss cin wex wn nnsuc eldifi
      adantl elndif eleq1 biimpcd pm2.16d comm syl imp sylan13 exp adantrd
      adantr ax-17 hba1 hban impexp eleq1a syl3 sylbir wel pm3.26 neldif minel
      sylan5 jca wtr word ordom ordtr ax-mp visset trsuc mpan syl6bi sylan
      exp32 sucid eleq2 mpbiri syl5 pm2.43a imp32 com23 exp4a pm2.43d ax-4
      19.23ad mpd eldifn a1i pm2.65d nexd difss tz7.5 nsyl2 ssdif0 sylibr ) DBE
      ZAFZGEZWRBEZWRHZBEZIIZAJZKZGBLZDMZGBNXECFZXFEZXFXHODMZKZCPZXGXEXKCXEXKXHB
      EZXEXKXMXEXKKZXHXAMZAPZXMXEXKXPWQXKXPIXDWQXIXPXJWQXIXPXHGEZXHDMZQZXPWQXIK
      AXHRXIXQWQXHGBSZTWQXIXSWQDXFEZQZXIXSIDBGUAXIYBXSXIXRYAXRXIYAXHDXFUBUCUDUE
      UFUGUHUIUJUKUGXNXOXMAXEXKAWQXDAWQAULXCAUMUNXKAULUNXMAULXEXKXOXMIZWQXCXKYC
      IZXDXCYDWQXCXOXKXMXCXOXKXMIXCXOXOXKXMXCXOXKKZXOXMXCWSWTKZYCYEXCYFXBIYFYCI
      WSWTXBUOXBYCYFXABXHUPUQURXOXIXJYFXOXQXJYFIZXIXQXOYGXOXQXOYGIXOXQKZACUSZYG
      XOYHYIXJYFWSYIXJKZYFYHWSYJKWSWTWSYJUTWSWRXFEQWTYJWRGBVAWRXHXFVBVCVDXOXQWS
      XOXQXAGEZWSXHXAGUBGVEZYKWSGVFZYLVGGVHVIGWRAVJZVKVLVMUGVNVOXOYIWRXAEWRYNVP
      XHXAWRVQVRVSUIVTXTVSWAVSWBWCWDWBTXCAWEVCUGWFWGUIXKXMQZIXEXIYOXJXHGBWHUKWI
      WJWKXFGNZXGQZXLGBWLYMYPYQKXLVGCGXFWMVLVLWNGBWOWP $.
      $([28-Jun-94]$)
  $}

  $( A class belongs to omega iff its successor does. $)
  peano2b $p |- ( A e. om <-> suc A e. om ) $=
    ( com wcel csuc c0 cded wceq suceq eleq1d peano1 elimel peano2 dedth cvv
    wi elnn sucidg sylan exp wn sucprc biimprd pm2.61i impbi ) ABCZADZBCZUEUGUE
    AEFZDZBCAEAUHGUFUIBAUHHIUHAEBJKLMANCZUGUEOUJUGUEAUFCUGUEUJAUFPANQRSUJTZUEUG
    UKAUFBAUAIUBUCUD $.
    $([3-Dec-95]$)

  ${
    $d x A $.
    $( A natural number is either 0 or a successor. $)
    nn0suc $p |- ( A e. om -> ( A = (/) \/ E. x e. om A = suc x ) ) $=
      ( com wcel c0 wceq cv csuc wex wo wrex wn nnsuc exp orrd wa eleq1
      biimpcd peano2b syl6ibr ancrd 19.22dv df-rex imlord mpd ) BCDZBEFZBAGZHZF
      ZAIZJUGUJACKZJUFUGUKUFUGLUKABMNOUFUKULUGUFUKUHCDZUJPZAIULUFUJUNAUFUJUMUFU
      JUICDZUMUJUFUOBUICQRUHSTUAUBUJACUCTUDUE $.
      $([27-May-98]$)
  $}

  ${
    $d x A $.
    findOBS.1 $e |- ( A (_ om /\ (/) e. A /\
                 A. x ( x e. om -> ( x e. A -> suc x e. A ) ) ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of Takeuti and Zaring, p. 43. $)
    findOBS   $p |- A = om $=
      ( com wss c0 wcel cv csuc wi wal w3a 3simp1 ax-mp peano5 3adant1 eqssi )
      BDBDEZFBGZAHZDGTBGTIBGJJAKZLZRCRSUAMNUBDBEZCSUAUCRABOPNQ $.
      $([4-Aug-94]$)
  $}

  ${
    $d x A $.
    find.1 $e |- ( A (_ om /\ (/) e. A /\
                 A. x ( x e. A -> suc x e. A ) ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of Takeuti and Zaring, p. 43.  Simpler hypothesis was suggested
       by "Colin" on 1-Oct-2001. $)
    find   $p |- A = om $=
      ( com wss c0 wcel cv csuc wi wal w3a 3simp1 ax-mp wa ax-1 19.20i imlan
      3anass 3imtr4 peano5 3adant1 eqssi ) BDBDEZFBGZAHZBGUFIBGJZAKZLZUDCUDUEUH
      MNUDUEUFDGZUGJZAKZLZDBEZUIUMCUDUEUHOZOUDUEULOZOUIUMUOUPUDUHULUEUGUKAUGUJP
      QRRUDUEUHSUDUEULSTNUEULUNUDABUAUBNUC $.
      $([1-Dec-01]$)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    finds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    finds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    finds.5 $e |- ps $.
    $( Induction hypothesis. $)
    finds.6 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema). $)
    finds $p |- ( A e. om -> ta ) $=
      ( com wcel cab c0 cv csuc wi wal wss zfnull abcl mpbir visset sucex
      3imtr4g ax-gen peano5 mp2an ssi abclg mpbid ) HOPHAFQZPEOUPHRUPPZGSZOPZUR
      UPPZURTZUPPZUAUAZGUBOUPUCUQBMABFRUDIUEUFVCGUSCDUTVBNACFURGUGZJUEADFVAURVD
      UHKUEUIUJGUPUKULUMAEFHOLUNUO $.
      $([14-Apr-95]$)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsgNEW.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsgNEW.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsgNEW.3 $e |- ( ( x = suc y /\ B (_ x ) -> ( ph <-> th ) ) $.
    findsgNEW.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsgNEW.5 $e |- ps $.
    $( Induction hypothesis. $)
    findsgNEW.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Generalized Principle of Finite Induction (inference schema).
       Remark, p. 57 of Takeuti and Zaring, limited to natural numbers. $)
    findsgNEW $p |- ( ( ( A e. om /\ B e. om ) /\ B (_ A ) -> ta ) $=
      ( com wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3 pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq bitrd a1i con onsssuct onelpsst tz7.24 sylan5 nnont syl2an ancoms
      ax-1 syl8 a2d com23 sylbird annim syl5ibr pm2.21 a1d wex visset sucex
      eqvinc mpbiri eqimss cleqcoms mpbii 19.23aiv sylbi ja pm2.61d2 finds
      imp31 ) HPQIPQZIHRZEWPIFSZRZATZTWPIUARZBTZTWPIGSZRZCTZTWPIXCUBZRZDTZTWPWQ
      ETZTFGHWRUAUCZWTXBWPIUAUCZXJWTXBUDZTXKXJXLXKXJUEWSXAABXJWSXAUDXKWRUAIUFZU
      GXKXJABUDZXKXJWRIUCZXNIUAWRUHJUIUJUKULXKUMZXJXLXJWTXAATXPXBXJWSXAAXMUNXPX
      AABXPXAXNXAXKIUOUPUQURUSULUTVAFGVBZWTXEWPXQWSXDACWRXCIUFKUKVAWRXFUCZWTXHW
      PXRWTWSDTXHXRWSADXRWSADUDZLULURXRWSXGDWRXFIUFUNVCVAWRHUCZWTXIWPXTWSWQAEWR
      HIUFMUKVAXBWPBXANVDVDXCPQZWPXEXHYAWPXEXHTZYAWPUEZXGIXFUCZTZYBYCXGYDUMUEZY
      BYEUMYCYFXDYBWPYAXDYFUDZIVEQZXCVEQZYGWPYAYHYIUEXDIXFQZYFIXCVFYHXFVEQYJYFU
      DYIIXFVGXCVHVIVCIVJXCVJVKVLYCXEXDXHYCXDCXHYCXDCDXHYCXDCDTOULDXGVMVNVOVPVQ
      XGYDVRVSXGYDYBXGUMXHXEXGDVTWAYDXHXEYDDXGDXFIXFIUCXRXOUEZFWBDFXFIXCGWCWDWE
      YKDFXRXODXRXOATXODTXOABNJWFXRXOADXRXOXSXRWSXSXOLWSIWRIWRWGWHVIULURWIUJWJW
      KWHWAWAWLWMULVOWNWO $.
      $([21-Mar-96]$)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsg.5 $e |- ps $.
    $( Induction hypothesis. $)
    findsg.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Generalized Principle of Finite Induction (inference schema).
       Remark, p. 57 of Takeuti and Zaring, limited to natural numbers. $)
    findsg $p |- ( ( ( A e. om /\ B e. om ) /\ B (_ A ) -> ta ) $=
      ( com wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3 pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq a1i con onsssuct onelpsst tz7.24 sylan5 bitrd nnont syl2an ancoms
      ax-1 syl8 a2d com23 sylbird annim syl5ibr pm2.21 a1d wex visset sucex
      eqvinc mpbiri syl5bi 19.23aiv sylbi cleqcoms ja pm2.61d2 finds imp31 ) HP
      QIPQZIHRZEWOIFSZRZATZTWOIUARZBTZTWOIGSZRZCTZTWOIXBUBZRZDTZTWOWPETZTFGHWQU
      AUCZWSXAWOIUAUCZXIWSXAUDZTXJXIXKXJXIUEWRWTABXIWRWTUDXJWQUAIUFZUGXJXIABUDZ
      XJXIWQIUCZXMIUAWQUHJUIUJUKULXJUMZXIXKXIWSWTATXOXAXIWRWTAXLUNXOWTABXOWTXMW
      TXJIUOUPUQURUSULUTVAFGVBZWSXDWOXPWRXCACWQXBIUFKUKVAWQXEUCZWSXGWOXQWRXFADW
      QXEIUFLUKVAWQHUCZWSXHWOXRWRWPAEWQHIUFMUKVAXAWOBWTNVCVCXBPQZWOXDXGXSWOXDXG
      TZXSWOUEZXFIXEUCZTZXTYAXFYBUMUEZXTYCUMYAYDXCXTWOXSXCYDUDZIVDQZXBVDQZYEWOX
      SYFYGUEXCIXEQZYDIXBVEYFXEVDQYHYDUDYGIXEVFXBVGVHVIIVJXBVJVKVLYAXDXCXGYAXCC
      XGYAXCCDXGYAXCCDTOULDXFVMVNVOVPVQXFYBVRVSXFYBXTXFUMXGXDXFDVTWAYBXGXDYBDXF
      DXEIXEIUCXQXNUEZFWBDFXEIXBGWCWDWEYIDFXQXNDXQADXNLXNABNJWFWGUJWHWIWJWAWAWK
      WLULVOWMWN $.
      $([16-Sep-95]$)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsgOBS.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsgOBS.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsgOBS.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsgOBS.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsgOBS.5 $e |- ps $.
    $( Induction hypothesis. $)
    findsgOBS.6 $e |- ( ( ( y e. om /\ B e. On ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Generalized Principle of Finite Induction (inference schema).
       Remark, p. 57 of Takeuti and Zaring, limited to natural numbers. $)
    findsgOBS $p |- ( ( ( A e. om /\ B e. On ) /\ B (_ A ) -> ta ) $=
      ( com wcel con wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir
      imp bi2imd exp wn birimd ss0 con3 pm2.21d pm5.74d sylan9bbr pm2.61i
      bilimd weq a1i onsssuct onelpsst tz7.24 sylan5 bitrd nnont ancoms ax-1
      syl8 a2d com23 sylbird annim syl5ibr pm2.21 a1d wex visset sucex eqvinc
      mpbiri syl5bi 19.23aiv sylbi cleqcoms ja pm2.61d2 finds imp31 ) HPQIRQZIH
      SZEWNIFTZSZAUAZUAWNIUBSZBUAZUAWNIGTZSZCUAZUAWNIXAUCZSZDUAZUAWNWOEUAZUAFGH
      WPUBUDZWRWTWNIUBUDZXHWRWTUEZUAXIXHXJXIXHUFWQWSABXHWQWSUEXIWPUBIUGZUHXIXHA
      BUEZXIXHWPIUDZXLIUBWPUIJUJUKULUMXIUNZXHXJXHWRWSAUAXNWTXHWQWSAXKUOXNWSABXN
      WSXLWSXIIUPUQURUSUTUMVAVBFGVCZWRXCWNXOWQXBACWPXAIUGKULVBWPXDUDZWRXFWNXPWQ
      XEADWPXDIUGLULVBWPHUDZWRXGWNXQWQWOAEWPHIUGMULVBWTWNBWSNVDVDXAPQZWNXCXFXRW
      NXCXFUAZXRWNUFZXEIXDUDZUAZXSXTXEYAUNUFZXSYBUNXTYCXBXSWNXRXBYCUEZWNXARQZYD
      XRWNYEUFXBIXDQZYCIXAVEWNXDRQYFYCUEYEIXDVFXAVGVHVIXAVJVHVKXTXCXBXFXTXBCXFX
      TXBCDXFXTXBCDUAOUMDXEVLVMVNVOVPXEYAVQVRXEYAXSXEUNXFXCXEDVSVTYAXFXCYADXEDX
      DIXDIUDXPXMUFZFWADFXDIXAGWBWCWDYGDFXPXMDXPADXMLXMABNJWEWFUKWGWHWIVTVTWJWK
      UMVNWLWM $.
      $([15-Sep-95]$)
  $}


  ${
    $d x y z $.  $d x A $.  $d x ps $.  $d x z ch $.  $d x th $.  $d x ta $.
    $d y z ph $.  $( ` z ` is dummy. $)
    $( Substitutions. $)
    tfinds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds.5 $e |- ps $.
    $( Induction hypothesis for successors. $)
    tfinds.6 $e |- ( y e. On -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds.7 $e |- ( Lim x -> ( A. y ( y e. x -> ch ) -> ph ) ) $.
    $( Principle of Transfinite Induction (inference schema). $)
    tfinds $p |- ( A e. On -> ta ) $=
      ( vz con cv wcel wlim wel wi wal wn c0 wceq csuc wex wo word eloni cuni
      w3a df-lim biimpr 3exp pm2.16 syl6 com23 orduninsuc biimprd pm2.15d
      syl6d df-or syl6ibr syl mpbiri a1d ax-17 hba1 hbim ordsuc visset elon
      sucex 3bitr4 sucid weq eleq1 bi2imd a4b1 mpi syl5 sylbir wsb eleq2
      birimd bialdv a13b sbequ ddel4 syl5bbr cbvalv hbsbv sbequ12 cbv4 3bitr4g
      19.23ai jaoi pm2.43a pm2.61d2 tfis2 vtoclga ) AEFHQLACFGJFRZQSZXDTZGFUAZC
      UBZGUCZAUBZXFUDZXEXJXEXKXDUEUFZXDGRZUGZUFZGUHZUIZXEXJUBZXEXDUJZXKXQUBXDUK
      XSXKXLUDZXPUBXQXSXKXTXDXDULUFZUDZXPXSXTXKYBXSXTYAXFUBXKYBUBXSXTYAXFXFXSXT
      YAUMXDUNUOUPYAXFUQURUSXSXPYAXSYAXPUDGXDUTVAVBVCXLXPVDVEVFXLXRXPXLXJXEXLAX
      IXLABMIVGVHVHXOXRGXEXJGXEGVIXIAGXHGVJAGVIVKVKXOXRXNQSZXDXNSZAUBZFUCZDUBZU
      BYCXMQSZYGXMUJXNUJYHYCXMVLXMGVMZVNXNXMYIVOVNVPYHCDYFNYFXMXNSZCXMYIVQYEYJC
      UBFGFGVRYDYJACXDXMXNVSJVTWAWBWCWDXOXEYCXJYGXDXNQVSXOXIYFADXOPFUAZAFPWEZUB
      ZPUCPRZXNSZYLUBZPUCXIYFXOYMYPPXOYKYOYLXDXNYNWFWGWHXHYMGPGPVRZXGYKCYLGPFWI
      YQAFGWEYLCAGPFWJACFGCFVIJWKWLVTWMYEYPFPYEPVIYOYLFYOFVIAFPWNVKFPVRYDYOAYLX
      DYNXNVSAFPWOVTWPWQKVTVTVGWRWSURWTOXAXBXC $.
      $([16-Apr-95]$)
  $}



