$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol. $)
  $c om $. $( Greek letter omega $)

  $( Extend class notation to include the class of natural numbers. $)
  com $a class om $.

  ${
    $d x y $.
    $( Define the class of natural numbers.  Our definition is a variant of the
       Definition of N of [BellMachover] p. 471.  Later, when we assume the
       Axiom of Infinity, we show ` om ` is a set in ~ omex , and ` om ` can
       then be defined per ~ dfom2 and ~ dfom3 .  Note: the natural numbers
       ` om ` are a subset of the ordinal numbers ~ df-on .  These are
       different from the natural number subset of complex numbers defined
       later ( ~ df-n ), although the two sets have analogous properties and
       operations defined on them. $)
    df-om $a |- om = { x | ( Ord x /\ A. y ( Lim y -> x e. y ) ) } $.
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    elom.1 $e |- A e. V $.
    $( Membership in omega.  The hypothesis can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 .  $)
    elom $p |- ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) $=
      ( vy cv word wlim wel wi wal wa wcel com wceq ordeq eleq1 bilimd bialdv
      bi2and df-om elab2 ) DEZFZAEZGZDAHZIZAJZKBFZUEBUDLZIZAJZKDBMCUBBNZUCUIUHU
      LUBBOUMUGUKAUMUFUJUEUBBUDPQRSDATUA $.
      $( [15-May-94] $)
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    $( Membership in omega.  The antecedent can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 .  $)
    elomg $p |- ( A e. B ->
                ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) ) $=
      ( vy cv com wcel word wlim wel wi wal wa eleq1 wceq ordeq bilimd bialdv
      bi2and visset elom vtoclbg ) DEZFGUCHZAEZIZDAJZKZALZMBFGBHZUFBUEGZKZALZMD
      BCUCBFNUCBOZUDUJUIUMUCBPUNUHULAUNUGUKUFUCBUENQRSAUCDTUAUB $.
      $( [25-Mar-95] $)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Omega is a subset of ` On ` . $)
    omsson $p |- om (_ On $=
      ( vx vy com con0 cv wcel word wlim wel wi wal visset elom pm3.26bd elon
      sylibr ssriv ) ACDAEZCFZRGZRDFSTBEHABIJBKBRALZMNRUAOPQ $.
      $( [13-Jun-94] $)
  $}

  ${
    $d x y A $.
    $( The class of natural numbers is a subclass of any (infinite) limit
       ordinal.  Exercise 1 of [TakeutiZaring] p. 44.  Remarkably, our proof
       does not require the Axiom of Infinity. $)
    limomss $p |- ( Lim A -> om (_ A ) $=
      ( vx vy word wlim com wss limord con0 wcel wceq wo wi ordeleqon wa cv
      wel wal limeq eleq2 bi2imd cla4gv visset elom pm3.27bd syl5 com23 imp
      ssrdv exp omsson sseq2 mpbiri a1d jaoi sylbi mpcom ) ADZAEZFAGZAHURAIJZAI
      KZLUSUTMZANVAVCVBVAUSUTVAUSOBFAVAUSBPZFJZVDAJZMVAVEUSVFVACPZEZBCQZMZCRZUS
      VFMZVEVJVLCAIVGAKVHUSVIVFVGASVGAVDTUAUBVEVDDVKCVDBUCUDUEUFUGUHUIUJVBUTUSV
      BUTFIGUKAIFULUMUNUOUPUQ $.
      $( [4-Nov-03] $) $( [30-Oct-03] $)
  $}

  $( A natural number is an ordinal number. $)
  nnont $p |- ( A e. om -> A e. On ) $=
    ( com con0 omsson sseli ) BCADE $.
    $( [27-Jun-94] $)

  ${
    nnon.1 $e |- A e. om $.
    $( A natural number is an ordinal number. $)
    nnon $p |- A e. On $=
      ( com wcel con0 nnont ax-mp ) ACDAEDBAFG $.
      $( [27-Jun-94] $)
  $}

  $( A natural number is ordinal. $)
  nnord $p |- ( A e. om -> Ord A ) $=
    ( com wcel con0 word nnont eloni syl ) ABCADCAEAFAGH $.
    $( [17-Oct-95] $)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( Omega is ordinal.  Theorem 7.32 of [TakeutiZaring] p. 43. $)
    ordom $p |- Ord om $=
      ( vy vx vz com wtr con0 wss word wel cv wcel wa wi wal dftr2 wlim
      ordelord nnord sylan ancoms trel exp3a com12 limord ordtr syl syl5 a2d
      19.20dv visset elom pm3.27bd imp jca sylibr ax-gen mpgbir omsson ordon
      trssord mp3an ) DEZDFGFHDHVBABIZBJZDKZLZAJZDKZMZBNAABDOVIBVFVGHZCJZPZACIZ
      MZCNZLVHVFVJVOVEVCVJVDHZVCVJVEVDVGQVDRSTVCVEVOVCVLBCIZMZCNZVOVEVCVRVNCVCV
      LVQVMVCVKEZVQVMMZVLVTVCWAVTVCVQVMVKVGVDUAUBUCVLVKHVTVKUDVKUEUFUGUHUIVEVPV
      SCVDBUJUKULUGUMUNCVGAUJUKUOUPUQURUSDFUTVA $.
      $( [18-Oct-95] $)
  $}

  $( A member of a natual number is a natural number. $)
  elnn $p |- ( ( A e. B /\ B e. om ) -> A e. om ) $=
    ( com wtr wcel wa wi word ordom ordtr ax-mp trel ) CDZABEBCEFACEGCHMICJKCAB
    LK $.
    $( [21-Jun-98] $)

  $( The class of natural numbers ` om ` is either an ordinal number (if we
     accept the Axiom of Infinity) or the proper class of all ordinals (if we
     deny the Axiom of Infinity).  Remark of [TakeutiZaring] p. 43. $)
  omon $p |- ( om e. On \/ om = On ) $=
    ( com word con0 wcel wceq wo ordom ordeleqon mpbi ) ABACDACEFGAHI $.
    $( [10-May-98] $)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
  $( A natural number is not a limit ordinal. $)
  nnlim $p |- ( A e. om -> -. Lim A ) $=
    ( vx com wcel wlim word wn nnord ordeirr syl cv wi wal wa elomg ibi
    pm3.27d wceq limeq eleq2 bi2imd cla4gv mpd mtod ) ACDZAEZAADZUEAFZUGGAHAIJU
    EBKZEZAUIDZLZBMZUFUGLZUEUHUMUEUHUMNBACOPQULUNBACUIARUJUFUKUGUIASUIAATUAUBUC
    UD $.
    $( [18-Oct-95] $)
  $}

  $( Omega is a limit ordinal.  Theorem 2.8 of [BellMachover] p. 473.
     Our proof does not require the axiom of Infinity. $)
  limom $p |- Lim om $=
    ( vx com word wlim ordom con0 wcel wceq wo ordeleqon wn ordeirr ax-mp cv
    wi wal wa elomg wss wb ordtri1 adantr ordsseleq biimpd nnlim a1i limeq
    con3d com12 jaod sylan9 sylbird a3d mpan12 limord sylan exp pm2.43b
    19.21aiv jctil syl5bir mt3i limon mpbiri jaoi sylbi ) BCZBDZEVGBFGZBFHZIVHB
    JVIVHVJVIVHBBGZVGVKKEBLMVIVKVGANZDZBVLGZOZAPZQVHKZABFRVQVPVGVQVOAVQVMVNVMVQ
    VOVLCZVQVOVMVRVGVQVOEVRVGQZVQQZVNVMVTVNKZVLBSZVMKZVSWBWATVQVLBUAUBVSWBVLBGZ
    VLBHZIZVQWCVSWBWFVLBUCUDVQWDWCWEWDWCOVQVLUEUFWEVQWCWEVMVHWEVMVHVLBUGUDUHUIU
    JUKULUMUNVLUOUPUQURUSEUTVAVBVJVHFDVCBFUGVDVEVFM $.
    $( [26-Mar-95] $)

  $( A class belongs to omega iff its successor does. $)
  peano2b $p |- ( A e. om <-> suc A e. om ) $=
    ( com wlim wcel csuc wb limom limsuc ax-mp ) BCABDAEBDFGBAHI $.
    $( [3-Dec-95] $)

  ${
    $d x y A $.
    $( A non-zero natural number is a successor. $)
    nnsuc $p |- ( ( A e. om /\ -. A = (/) ) -> E. x e. om A = suc x ) $=
      ( com wcel c0 wceq wn wa cv csuc con0 wrex wlim nnlim adantr word wi
      cuni wb orduninsuc w3a df-lim biimpr 3exp imp sylbird nnord sylan mt3d
      wex eleq1 biimpcd peano2b syl6ibr ancrd adantld 19.22dv df-rex 3imtr4g
      mpd ) BCDZBEFGZHZBAIZJZFZAKLZVFACLZVCVGBMZVAVIGVBBNOBPZVBVGGZVIQVAVJVBHVK
      BBRFZVIVJVLVKSVBABTOVJVBVLVIQVJVBVLVIVIVJVBVLUABUBUCUDUEUFBUGUHUIVAVGVHQV
      BVAVDKDZVFHZAUJVDCDZVFHZAUJVGVHVAVNVPAVAVFVPVMVAVFVOVAVFVECDZVOVFVAVQBVEC
      UKULVDUMUNUOUPUQVFAKURVFACURUSOUT $.
      $( [21-Feb-04] $) $( [18-Feb-04] $)
  $}

  $( Zero is a natural number.  One of Peano's 5 postulates for arithmetic.
     Proposition 7.30(1) of [TakeutiZaring] p. 42. $)
  peano1 $p |- (/) e. om $=
    ( com wlim c0 wcel limom 0ellim ax-mp ) ABCADEAFG $.
    $( [15-May-94] $)

  $( The successor of any natural number is a natural number.  One of Peano's
     5 postulates for arithmetic.  Proposition 7.30(2) of [TakeutiZaring]
     p. 42. $)
  peano2 $p |- ( A e. om -> suc A e. om ) $=
    ( com wcel csuc peano2b biimp ) ABCADBCAEF $.
    $( [3-Sep-03] $)

  $( The successor of any natural number is not zero.  One of Peano's 5
     postulates for arithmetic.  Proposition 7.30(3) of [TakeutiZaring]
     p. 42. $)
  peano3 $p |- ( A e. om -> -. suc A = (/) ) $=
    ( csuc c0 wceq wn com wcel nsuceq0 a1i ) ABCDEAFGAHI $.
    $( [3-Sep-03] $)

  $( Two natural numbers are equal iff their successors are equal, i.e. the
     successor function is one-to-one.  One of Peano's 5 postulates for
     arithmetic.  Proposition 7.30(4) of [TakeutiZaring] p. 43. $)
  peano4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A = suc B <-> A = B ) ) $=
    ( con0 wcel csuc wceq wb com suc11 nnont syl2an ) ACDBCDAEBEFABFGAHDBHDABIA
    JBJK $.
    $( [3-Sep-03] $)

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( The induction postulate: any set containing zero and closed under the
       successor operation contains all natural numbers.  One of Peano's 5
       postulates for arithmetic.  Proposition 7.30(5) of [TakeutiZaring]
       p. 43. $)
    peano5 $p |- ( ( (/) e. A /\
                 A. x e. om ( x e. A -> suc x e. A ) ) -> om (_ A ) $=
      ( vy c0 wcel cv csuc wi com wral wa cdif wceq wss cin wrex wn eldifn
      adantl nnsuc eldifi elndif eleq1 biimpcd con3d com12 syl imp sylan13
      adantlr adantr hbra1 ax-17 hban ra4 wel visset sucid eleq2 mpbiri
      peano2b syl6bbr neldif minel sylan5 exp32 syl6bi com23 mpd syl5 imp3a
      eleq1a syl34d com13 sylan9 r19.23ad a1i imp41 exp mtod nrexdv difss word
      ordom tz7.5 mpan nsyl2 ssdif0 sylibr ) DBEZAFZBEZWKGZBEZHZAIJZKZIBLZDMZIB
      NWQWRCFZODMZCWRPZWSWQXACWRWQWTWREZKZXAWTBEZXCXEQWQWTIBRSXDXAXEXDXAKWTWMMZ
      AIPZXEXDXGXAWJXCXGWPWTIEZWTDMZQZXGWJXCKAWTTXCXHWJWTIBUAZSWJXCXJWJDWREZQZX
      CXJHDBIUBXCXMXJXCXIXLXIXCXLWTDWRUCUDUEUFUGUHUIUJUKWJWPXCXAXGXEHZWPXCXAXNH
      HHWJWPXCXAXNWPXCXAKZKXFXEAIWPXOAWOAIULXOAUMUNXEAUMWPWKIEZWOXOXFXEHWOAIUOX
      FWOXOXEXFXOWLWNXEXFXCXAWLXFXHXAWLHZXCXFACUPZXHXQHXFXRWKWMEWKAUQURWTWMWKUS
      UTXFXHXRXQXFXHXPXRXQHXFXHWMIEXPWTWMIUCWKVAVBXPXRXAWLXPWKWREQWLXRXAKWKIBVC
      WKWTWRVDVEVFVGVHVIXKVJVKWNXFXEWMBWTVLUFVMVNVOVPVFVQVRVIVSVTWAWRINZWSQZXBI
      BWBIWCXSXTKXBWDCIWRWEWFWFWGIBWHWI $.
      $( [2-Mar-04] $) $( [18-Feb-04] $)
  $}

  ${
    $d x A $.
    $( A natural number is either 0 or a successor. $)
    nn0suc $p |- ( A e. om -> ( A = (/) \/ E. x e. om A = suc x ) ) $=
      ( com wcel c0 wceq cv csuc wrex wn nnsuc exp orrd ) BCDZBEFZBAGHFACIZNOJP
      ABKLM $.
      $( [27-May-98] $)
  $}

  ${
    $d x A $.
    find.1 $e |- ( A (_ om /\ (/) e. A /\ A. x e. A suc x e. A ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of [TakeutiZaring] p. 43.  The simpler hypothesis shown here was
       suggested in an email from "Colin" on 1-Oct-01.  The hypothesis states
       that ` A ` is a set of natural numbers, zero belongs to ` A ` , and
       given any member of ` A ` the member's successor also belongs to ` A ` .
       The conclusion is that every natural number is in ` A ` . $)
    find $p |- A = om $=
      ( com wss c0 wcel cv csuc wral w3a 3simp1 ax-mp wi wa ax-1 r19.20si
      ralcom3 sylib imlan 3anass 3imtr4 peano5 3adant1 eqssi ) BDBDEZFBGZAHZIBG
      ZABJZKZUFCUFUGUJLMUFUGUHBGUINADJZKZDBEZUKUMCUFUGUJOZOUFUGULOZOUKUMUOUPUFU
      JULUGUJUHDGZUINZABJULUIURABUIUQPQUIABDRSTTUFUGUJUAUFUGULUAUBMUGULUNUFABUC
      UDMUE $.
      $( [5-Mar-04] $) $( [22-Feb-04] $)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    finds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    finds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    finds.5 $e |- ps $.
    $( Induction hypothesis. $)
    finds.6 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136. $)
    finds $p |- ( A e. om -> ta ) $=
      ( com wcel cab c0 cv csuc wi wral wss 0ex elab mpbir visset sucex
      3imtr4g rgen peano5 mp2an sseli elabg mpbid ) HOPHAFQZPEOUPHRUPPZGSZUPPZU
      RTZUPPZUAZGOUBOUPUCUQBMABFRUDIUEUFVBGOUROPCDUSVANACFURGUGZJUEADFUTURVCUHK
      UEUIUJGUPUKULUMAEFHOLUNUO $.
      $( [14-Apr-95] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsg.5 $e |- ( B e. om -> ps ) $.
    $( Induction hypothesis. $)
    findsg.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.  The
       basis of this version is an arbitrary natural number ` B ` instead of
       zero. $)
    findsg $p |- ( ( ( A e. om /\ B e. om ) /\ B (_ A ) -> ta ) $=
      ( com wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi
      cleqcoms syl3 com4r con0 onsssuc onelpsst suceloni sylan5 bitrd nnont
      syl2an ancoms ax-1 syl8 a2d com23 sylbird annim syl5ibr pm2.61d finds
      imp31 ) HPQIPQZIHRZEWOIFSZRZATZTWOIUARZBTZTWOIGSZRZCTZTWOIXBUBZRZDTZTWOWP
      ETZTFGHWQUAUCZWSXAWOIUAUCZXIWSXAUDZTXJXIXKXJXIUEWRWTABXIWRWTUDXJWQUAIUFZU
      GXJXIABUDZXJXIWQIUCZXMIUAWQUHJUIUJUKULXJUMZXIXKXIWSWTATXOXAXIWRWTAXLUNXOW
      TABXOWTXMWTXJIUOUPUQURUSULUTVAFGVBZWSXDWOXPWRXCACWQXBIUFKUKVAWQXEUCZWSXGW
      OXQWRXFADWQXEIUFLUKVAWQHUCZWSXHWOXRWRWPAEWQHIUFMUKVAWOBWTNVCXBPQZWOXDXGXS
      WOXDXGTZXSWOUEZXFIXEUCZTZXTWOYCXTTXSYCXDXFWODYCXFWODTZTXDYBYDXFYDXEIXEIUC
      XQXNUEZFVDYDFXEIXBGVEVFVGYEYDFXNWOAXQDXNABWOJNVHXQADLVIVJVKVLVMVNVCVOUGYA
      XFYBUMUEZXTYCUMYAYFXCXTWOXSXCYFUDZIVPQZXBVPQZYGWOXSYHYIUEXCIXEQZYFIXBVQYH
      XEVPQYJYFUDYIIXEVRXBVSVTWAIWBXBWBWCWDYAXDXCXGYAXCCXGYAXCCDXGYAXCCDTOULDXF
      WEWFWGWHWIXFYBWJWKWLULWGWMWN $.
      $( [16-Sep-95] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    finds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis. $)
    finds2.5 $e |- ( y e. om -> ( ta -> ( ch -> th ) ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136.  $)
    finds2 $p |- ( x e. om -> ( ta -> ph ) ) $=
      ( cv com wcel wi cab c0 csuc wral wss 0ex wceq bilimd elab mpbir a2d
      visset weq sucex 3imtr4g rgen peano5 mp2an sseli abid sylib ) FMZNOUREAPZ
      FQZOUSNUTURRUTOZGMZUTOZVBSZUTOZPZGNTNUTUAVAEBPZKUSVGFRUBURRUCABEHUDUEUFVF
      GNVBNOZECPZEDPZVCVEVHECDLUGUSVIFVBGUHZFGUIACEIUDUEUSVJFVDVBVKUJURVDUCADEJ
      UDUEUKULGUTUMUNUOUSFUPUQ $.
      $( [29-Nov-02] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    findes.1 $e |- [ (/) / x ] ph $.
    findes.2 $e |- ( x e. om -> ( ph -> [ suc x / x ] ph ) ) $.
    $( Finite induction with explicit substitution.  The first hypothesis is
       the basis, and the second is the induction hypothesis.  Theorem Schema
       22 of [Suppes] p. 136. (Contributed by Raph Levien, 9-Jul-03.) $)
    findes $p |- ( x e. om -> ph ) $=
      ( vz vy wsb c0 wsbc cv csuc dfsbcq sbequ weq sbid syl6bb com wcel wi
      ax-17 hbs1 visset sucex hbsbcv hbim eleq1 sbequ12 wceq wb suceq syl
      bi2imd chv2 finds ) ABEGZABHIABFGZABFJZKZIZAEFBJZABEJZHLAEFBMABVAURLEBNUO
      ABBGAAEBBMABOPCUTQRZAABUTKZIZSZSUQQRZUPUSSZSBFVFVGBVFBTUPUSBABFUAABURUQFU
      BUCUDUEUEBFNZVBVFVEVGUTUQQUFVHAUPVDUSABFUGVHVCURUHVDUSUIUTUQUJABVCURLUKUL
      ULDUMUN $.
      $( [29-Jul-03] $)
  $}

  ${
    $d x y z $.  $d x A $.  $d x ps $.  $d x z ch $.  $d x th $.  $d x ta $.
    $d y z ph $.  $( ` z ` is dummy. $)
    $( Substitutions. $)
    tfinds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds.5 $e |- ps $.
    $( Induction hypothesis for successors. $)
    tfinds.6 $e |- ( y e. On -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds.7 $e |- ( Lim x -> ( A. y e. x ch -> ph ) ) $.
    $( Principle of Transfinite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the
       substitutions we need.  The last three are the basis, the induction
       hypothesis for successors, and the induction hypothesis for limit
       ordinals.  Theorem Schema 4 of [Suppes] p. 197. $)
    tfinds $p |- ( A e. On -> ta ) $=
      ( vz cv con0 wcel wlim wral wi wn c0 wceq csuc wrex wo word eloni cuni
      w3a df-lim biimpr 3exp con3 syl6 com23 orduninsuc biimprd con1d syl6d
      df-or syl6ibr syl mpbiri a1d ax-17 hbra1 hbim wa wsb raleq weq sbequ
      sbie syl5bbr cbvralv hbs1 sbequ12 cbvral 3bitr4g biimpd visset sucid
      rcla4v mpi syl5 sylan9r wb adantl sylibrd exp r19.23ai jaoi pm2.43a
      pm2.61d2 tfis3 ) ACEFGHJLFQZRSZWSTZCGWSUAZAUBZXAUCZWTXCWTXDWSUDUEZWSGQZUF
      ZUEZGRUGZUHZWTXCUBZWTWSUIZXDXJUBWSUJXLXDXEUCZXIUBXJXLXDXMWSWSUKUEZUCZXIXL
      XMXDXOXLXMXNXAUBXDXOUBXLXMXNXAXAXLXMXNULWSUMUNUOXNXAUPUQURXLXIXNXLXNXIUCG
      WSUSUTVAVBXEXIVCVDVEXEXKXIXEXCWTXEAXBXEABMIVFVGVGXHXKGRWTXCGWTGVHXBAGCGWS
      VIAGVHVJVJXFRSZXHXKXPXHVKZXCWTXQXBDAXHXBAFXGUAZXPDXHXBXRXHAFPVLZPWSUAXSPX
      GUAXBXRXSPWSXGVMCXSGPWSGPVNAFGVLXSCAGPFVOACFGCFVHJVPVQVRAXSFPXGAPVHAFPVSA
      FPVTWAWBWCXPCDXRNXRXFXGSCXFGWDWEACFXFXGJWFWGWHWIXHADWJXPKWKWLVGWMWNWOUQWP
      OWQWR $.
      $( [6-Mar-04] $) $( [16-Apr-95] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    tfindsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    tfindsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg.6 $e |- ( ( ( y e. On /\ B e. On ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg.7 $e |- ( ( ( Lim x /\ B e. On ) /\ B (_ x ) ->
                   ( A. y e. x ( B (_ y -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` B ` instead of zero. $)
    tfindsg $p |- ( ( ( A e. On /\ B e. On ) /\ B (_ A ) -> ta ) $=
      ( con0 wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3i pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi
      cleqcoms syl3 com4r onsssuc onelpsst suceloni sylan5 bitrd ancoms ax-1
      syl8 a2d com23 sylbird annim syl5ibr pm2.61d wlim wral pm2.27 r19.20sdv
      ad2antlr syld exp31 com3l com4t tfinds imp31 ) HQRIQRZIHSZEXBIFTZSZAUAZUA
      XBIUBSZBUAZUAXBIGTZSZCUAZUAZXBIXIUCZSZDUAZUAXBXCEUAZUAFGHXDUBUDZXFXHXBIUB
      UDZXQXFXHUEZUAXRXQXSXRXQUFXEXGABXQXEXGUEXRXDUBIUGZUHXRXQABUEZXRXQXDIUDZYA
      IUBXDUIJUJUKULUMXRUNZXQXSXQXFXGAUAYCXHXQXEXGAXTUOYCXGABYCXGYAXGXRIUPUQURU
      SUTUMVAVBFGVCZXFXKXBYDXEXJACXDXIIUGKULVBXDXMUDZXFXOXBYEXEXNADXDXMIUGLULVB
      XDHUDZXFXPXBYFXEXCAEXDHIUGMULVBXBBXGNVDXIQRZXBXKXOYGXBXKXOUAZYGXBUFZXNIXM
      UDZUAZYHXBYKYHUAYGYKXKXNXBDYKXNXBDUAZUAXKYJYLXNYLXMIXMIUDYEYBUFZFVEYLFXMI
      XIGVFVGVHYMYLFYBXBAYEDYBABXBJNVIYEADLVJVKVLVMVNVOVDVPUHYIXNYJUNUFZYHYKUNY
      IYNXJYHXBYGXJYNUEXBYGUFXJIXMRZYNIXIVQXBXMQRYOYNUEYGIXMVRXIVSVTWAWBYIXKXJX
      OYIXJCXOYIXJCDXOYIXJCDUAOUMDXNWCWDWEWFWGXNYJWHWIWJUMWEXBXEXDWKZXLGXDWLZAY
      PXBXEYQAUAZYPXBXEYRYPXBUFXEUFYQXKGXDWLZAXBYQYSUAYPXEXBXLXKGXDXBXKWMWNWOPW
      PWQWRWSWTXA $.
      $( [6-Mar-04] $) $( [5-Mar-04] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    tfindes.1 $e |- [ (/) / x ] ph $.
    tfindes.2 $e |- ( x e. On -> ( ph -> [ suc x / x ] ph ) ) $.
    tfindes.3 $e |- ( Lim y -> ( A. x e. y ph -> [ y / x ] ph ) ) $.
    $( Transfinite Induction with explicit substitution.  The first hypothesis
       is the basis, the second is the induction hypothesis for successors, and
       the third is the induction hypothesis for limit ordinals.    Theorem
       Schema 4 of [Suppes] p. 197. $)
    tfindes $p |- ( x e. On -> ph ) $=
      ( vz wsb c0 wsbc cv csuc dfsbcq sbequ weq sbid syl6bb con0 wcel wi ax-17
      hbs1 visset sucex hbsbcv hbim eleq1 sbequ12 wceq wb suceq syl bi2imd
      chv2 wlim wral cbvral syl5ibr tfinds ) ABCHZABIJABGHZABGKZLZJZACGBKZABCKZ
      IMACGBNABVFVCMCBOUTABBHAACBBNABPQDVERSZAABVELZJZTZTVBRSZVAVDTZTBGVKVLBVKB
      UAVAVDBABGUBZABVCVBGUCUDUEUFUFBGOZVGVKVJVLVEVBRUGVNAVAVIVDABGUHZVNVHVCUIV
      IVDUJVEVBUKABVHVCMULUMUMEUNVFUOABVFUPUTVAGVFUPFAVABGVFAGUAVMVOUQURUS $.
      $( [6-Mar-04] $) $( [5-Mar-04] $)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    tfinds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    tfinds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis for successors. $)
    tfinds2.5 $e |- ( y e. On -> ( ta -> ( ch -> th ) ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds2.6 $e |- ( Lim x -> ( ta -> ( A. y e. x ch -> ph ) ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first three hypotheses establish the substitutions we need.  The
       last three are the basis and the induction hypotheses (for successor and
       limit ordinals respectively).  Theorem Schema 4 of [Suppes] p. 197.  The
       wff ` ta ` is an auxilliary antecedent to help shorten proofs using this
       theorem. $)
    tfinds2 $p |- ( x e. On -> ( ta -> ph ) ) $=
      ( wi c0 wsbc 0ex cv wceq bilimd sbcie mpbir con0 wcel wsb csuc a2d sbimi
      cvv wb visset sbcel1 ax-mp sbim 3imtr3 weq bicomd eqcoms sucex bisb
      suceq sbcco2 bitr3 3imtr3g wlim wral r19.21v syl5ib ax-17 limeq sbie
      sbralie syl5ibr tfindes ) EANZFGVOFOPEBNZKVOVPFOQFRZOSABEHTUAUBVQUCUDZECN
      ZGFUEZEDNZGFUEZVOVOFVQUFZPZGRZUCUDZGFUEZVSWANZGFUEVRVTWBNWFWHGFWFECDLUGUH
      VQUIUDWGVRUJFUKZGVQUCUIULUMVSWAGFUNUOVSVOGVQWIGFUPCAECAUJFGFGUPACIUQURTZU
      AWBVOFWEUFZPZGFUEWDWLWAGFVOWAFWKWEGUKUSVQWKSADEJTUAUTVOFGWCWKVQWEVAVBVCVD
      WEVEZVSGVQVFZFGUEZVOFGUEZVOFWEVFVQVEZFGUEWNVONZFGUEWMWOWPNWQWRFGWQECGVQVF
      ZNVOWNWQEWSAMUGECGVQVGVHUHWQWMFGWMFVIVQWEVJVKWNVOFGUNUOVSVOGFWJVLVMVN $.
      $( [6-Sep-04] $) $( [4-Sep-04] $)
  $}

