$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol. $)
  $c om $. $( Greek letter omega $)

  $( Extend class notation to include the class of natural numbers. $)
  com $a class om $.

  ${
    $d x y $.
    $( Define the class of natural numbers.  Our definition is a variant of the
       Definition for N of [BellMachover] p. 471.  Later, when we assume the
       Axiom of Infinity, we show ` om ` is a set in ~ omex , and ` om ` can
       then be defined per ~ dfom2 and ~ dfom3 . $)
    df-om $a |- om = { x | ( Ord x /\ A. y ( Lim y -> x e. y ) ) } $.
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    elom.1 $e |- A e. V $.
    $( Membership in omega.  The hypothesis can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 .  $)
    elom $p |- ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) $=
      ( vy cv word wlim wel wi wal wa wcel com wceq ordeq eleq1 bilimd bialdv
      bi2and df-om vtoclab ) DEZFZAEZGZDAHZIZAJZKBFZUEBUDLZIZAJZKDBMCUBBNZUCUIU
      HULUBBOUMUGUKAUMUFUJUEUBBUDPQRSDATUA $.
      $([15-May-94]$)
  $}

  ${
    $d A x y $.  $( ` y ` is dummy. $)
    $( Membership in omega.  The antecedent can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 .  $)
    elomg $p |- ( A e. B ->
                ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) ) $=
      ( vy cv com wcel word wlim wel wi wal wa eleq1 wceq ordeq bilimd bialdv
      bi2and visset elom vtoclgb ) DEZFGUCHZAEZIZDAJZKZALZMBFGBHZUFBUEGZKZALZMD
      BCUCBFNUCBOZUDUJUIUMUCBPUNUHULAUNUGUKUFUCBUENQRSAUCDTUAUB $.
      $([25-Mar-95]$)
  $}

  ${
    $d x y $.  $( ` x y ` are dummy. $)
    $( Omega is a subset of ` On ` . $)
    omsson $p |- om (_ On $=
      ( vx vy com con0 cv wcel word wlim wel wi wal visset elom pm3.26bd elon
      sylibr ssriv ) ACDAEZCFZRGZRDFSTBEHABIJBKBRALZMNRUAOPQ $.
      $([13-Jun-94]$)
  $}

  ${
    $d x y A $.
    $( The class of natural numbers is a subclass of any (infinite) limit
       ordinal.  Exercise 1 of [TakeutiZaring] p. 44.  Remarkably, our proof
       does not require the Axiom of Infinity. $)
    limomss $p |- ( Lim A -> om (_ A ) $=
      ( vx vy word wlim com wss limord con0 wcel wceq wo wi ordeleqon wa cv
      wel wal limeq eleq2 bi2imd cla4gv visset elom pm3.27bd syl5 com23 imp
      ssrdv exp omsson sseq2 mpbiri a1d jaoi sylbi mpcom ) ADZAEZFAGZAHURAIJZAI
      KZLUSUTMZANVAVCVBVAUSUTVAUSOBFAVAUSBPZFJZVDAJZMVAVEUSVFVACPZEZBCQZMZCRZUS
      VFMZVEVJVLCAIVGAKVHUSVIVFVGASVGAVDTUAUBVEVDDVKCVDBUCUDUEUFUGUHUIUJVBUTUSV
      BUTFIGUKAIFULUMUNUOUPUQ $.
      $([4-Nov-03]$)
  $}

  $( A natural number is an ordinal number. $)
  nnont $p |- ( A e. om -> A e. On ) $=
    ( com con0 omsson ssi ) BCADE $.
    $([27-Jun-94]$)

  ${
    nnon.1 $e |- A e. om $.
    $( A natural number is an ordinal number. $)
    nnon   $p |- A e. On $=
      ( com wcel con0 nnont ax-mp ) ACDAEDBAFG $.
      $([27-Jun-94]$)
  $}

  $( A natural number is ordinal. $)
  nnord $p |- ( A e. om -> Ord A ) $=
    ( com wcel con0 word nnont eloni syl ) ABCADCAEAFAGH $.
    $([17-Oct-95]$)

  ${
    $d x y z $.  $( ` x y z ` are dummy. $)
    $( Omega is ordinal.  Theorem 7.32 of [TakeutiZaring] p. 43. $)
    ordom $p |- Ord om $=
      ( vy vx vz com wtr con0 wss word wel cv wcel wa wi wal dftr2 wlim
      ordelord nnord sylan ancoms trel exp3a comm limord ordtr syl syl5 a2d
      19.20dv visset elom pm3.27bd imp jca sylibr ax-gen mpgbir omsson ordon
      trssord mp3an ) DEZDFGFHDHVBABIZBJZDKZLZAJZDKZMZBNAABDOVIBVFVGHZCJZPZACIZ
      MZCNZLVHVFVJVOVEVCVJVDHZVCVJVEVDVGQVDRSTVCVEVOVCVLBCIZMZCNZVOVEVCVRVNCVCV
      LVQVMVCVKEZVQVMMZVLVTVCWAVTVCVQVMVKVGVDUAUBUCVLVKHVTVKUDVKUEUFUGUHUIVEVPV
      SCVDBUJUKULUGUMUNCVGAUJUKUOUPUQURUSDFUTVA $.
      $([18-Oct-95]$)
  $}

  $( A member of a natual number is a natural number. $)
  elnn $p |- ( ( A e. B /\ B e. om ) -> A e. om ) $=
    ( com wtr wcel wa wi word ordom ordtr ax-mp trel ) CDZABEBCEFACEGCHMICJKCAB
    LK $.
    $([21-Jun-98]$)

  $( The class of natural numbers ` om ` is either an ordinal number (if we
     accept the Axiom of Infinity) or the proper class of all ordinals (if we
     deny the Axiom of Infinity).  Remark of [TakeutiZaring] p. 43. $)
  omon $p |- ( om e. On \/ om = On ) $=
    ( com word con0 wcel wceq wo ordom ordeleqon mpbi ) ABACDACEFGAHI $.
    $([10-May-98]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
  $( A natural number is not a limit ordinal. $)
  nnlim $p |- ( A e. om -> -. Lim A ) $=
    ( vx com wcel wlim word wn nnord ordeirr syl cv wi wal wa elomg ibi
    pm3.27d wceq limeq eleq2 bi2imd cla4gv mpd mtod ) ACDZAEZAADZUEAFZUGGAHAIJU
    EBKZEZAUIDZLZBMZUFUGLZUEUHUMUEUHUMNBACOPQULUNBACUIARUJUFUKUGUIASUIAATUAUBUC
    UD $.
    $([18-Oct-95]$)
  $}

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    $( A non-zero natural number is a successor. $)
    nnsuc $p |- ( ( A e. om /\ -. A = (/) ) -> E. x A = suc x ) $=
      ( com wcel c0 wceq wn wa cv csuc wex wlim nnlim adantr word wi cuni wb
      orduninsuc w3a df-lim biimpr 3exp imp sylbird nnord sylan mt3d ) BCDZBEFG
      ZHBAIJFAKZBLZUIULGUJBMNBOZUJUKGZULPUIUMUJHUNBBQFZULUMUOUNRUJABSNUMUJUOULP
      UMUJUOULULUMUJUOTBUAUBUCUDUEBUFUGUH $.
      $([18-Oct-95]$)
  $}

  $( Omega is a limit ordinal.  Theorem 2.8 of [BellMachover] p. 473.
     Our proof does not require the axiom of Infinity. $)
  limom $p |- Lim om $=
    ( vx com word wlim ordom con0 wcel wceq wo ordeleqon wn ordeirr ax-mp cv
    wi wal wa elomg wss wb ordtri1 adantr ordsseleq biimpd nnlim a1i limeq
    pm2.16d comm jaod sylan9 sylbird a3d mpan12 limord sylan exp pm2.43b
    19.21aiv jctil syl5bir mt3i limon mpbiri jaoi sylbi ) BCZBDZEVGBFGZBFHZIVHB
    JVIVHVJVIVHBBGZVGVKKEBLMVIVKVGANZDZBVLGZOZAPZQVHKZABFRVQVPVGVQVOAVQVMVNVMVQ
    VOVLCZVQVOVMVRVGVQVOEVRVGQZVQQZVNVMVTVNKZVLBSZVMKZVSWBWATVQVLBUAUBVSWBVLBGZ
    VLBHZIZVQWCVSWBWFVLBUCUDVQWDWCWEWDWCOVQVLUEUFWEVQWCWEVMVHWEVMVHVLBUGUDUHUIU
    JUKULUMUNVLUOUPUQURUSEUTVAVBVJVHFDVCBFUGVDVEVFM $.
    $([26-Mar-95]$)

  $( A class belongs to omega iff its successor does. $)
  peano2b $p |- ( A e. om <-> suc A e. om ) $=
    ( com wlim wcel csuc wb limom limsuc ax-mp ) BCABDAEBDFGBAHI $.
    $([3-Dec-95]$)

  $( ` x ` is dummy. $)
  $( Zero is a natural number.  One of Peano's 5 postulates for arithmetic.
     Proposition 7.30(1) of [TakeutiZaring] p. 42. $)
  peano1 $p |- (/) e. om $=
    ( com wlim c0 wcel limom 0ellim ax-mp ) ABCADEAFG $.
    $([15-May-94]$)

  $( The successor of any natural number is a natural number.  One of Peano's
     5 postulates for arithmetic.  Proposition 7.30(2) of [TakeutiZaring]
     p. 42. $)
  peano2 $p |- ( A e. om -> suc A e. om ) $=
    ( com wcel csuc peano2b biimp ) ABCADBCAEF $.
    $([3-Sep-03]$)

  $( The successor of any natural number is not zero.  One of Peano's 5
     postulates for arithmetic.  Proposition 7.30(3) of [TakeutiZaring]
     p. 42. $)
  peano3 $p |- ( A e. om -> -. suc A = (/) ) $=
    ( csuc c0 wceq wn com wcel nsuceq0 a1i ) ABCDEAFGAHI $.
    $([3-Sep-03]$)

  $( Two natural numbers are equal iff their successors are equal, i.e. the
     successor function is one-to-one.  One of Peano's 5 postulates for
     arithmetic.  Proposition 7.30(4) of [TakeutiZaring] p. 43. $)
  peano4 $p |- ( ( A e. om /\ B e. om ) -> ( suc A = suc B <-> A = B ) ) $=
    ( con0 wcel csuc wceq wb com suc11 nnont syl2an ) ACDBCDAEBEFABFGAHDBHDABIA
    JBJK $.
    $([3-Sep-03]$)

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( The induction postulate: any set containing zero and closed under the
       successor operation contains all natural numbers.  One of Peano's 5
       postulates for arithmetic.  Proposition 7.30(5) of [TakeutiZaring]
       p. 43. $)
    peano5 $p |- ( ( (/) e. A /\
                 A. x ( x e. om -> ( x e. A -> suc x e. A ) ) ) ->
                 om (_ A ) $=
      ( vy c0 wcel cv com csuc wi wal wa cdif wceq wss cin wex wn nnsuc eldifi
      adantl elndif eleq1 biimpcd pm2.16d comm syl imp sylan13 exp adantrd
      adantr ax-17 hba1 hban impexp eleq1a syl3 sylbir wel pm3.26 neldif minel
      sylan5 jca peano2b syl6bbr biimpa sylan exp32 visset sucid eleq2 mpbiri
      syl5 pm2.43a imp32 com23 exp4a pm2.43d ax-4 19.23ad mpd eldifn a1i
      pm2.65d nexdv difss word ordom tz7.5 mpan nsyl2 ssdif0 sylibr ) DBEZAFZGE
      ZWPBEZWPHZBEZIIZAJZKZGBLZDMZGBNXCCFZXDEZXDXFODMZKZCPZXEXCXICXCXIXFBEZXCXI
      XKXCXIKZXFWSMZAPZXKXCXIXNWOXIXNIXBWOXGXNXHWOXGXNXFGEZXFDMZQZXNWOXGKAXFRXG
      XOWOXFGBSZTWOXGXQWODXDEZQZXGXQIDBGUAXGXTXQXGXPXSXPXGXSXFDXDUBUCUDUEUFUGUH
      UIUJUKUGXLXMXKAXCXIAWOXBAWOAULXAAUMUNXIAULUNXKAULXCXIXMXKIZWOXAXIYAIZXBXA
      YBWOXAXMXIXKXAXMXIXKIXAXMXMXIXKXAXMXIKZXMXKXAWQWRKZYAYCXAYDWTIYDYAIWQWRWT
      UOWTYAYDWSBXFUPUQURXMXGXHYDXMXOXHYDIZXGXOXMYEXMXOXMYEIXMXOKZACUSZYEXMYFYG
      XHYDWQYGXHKZYDYFWQYHKWQWRWQYHUTWQWPXDEQWRYHWPGBVAWPXFXDVBVCVDXMXOWQXMXOWS
      GEWQXFWSGUBWPVEVFVGVHVIXMYGWPWSEWPAVJVKXFWSWPVLVMVNUIVOXRVNVPVNVQVRVSVQTX
      AAVTVCUGWAWBUIXIXKQZIXCXGYIXHXFGBWCUKWDWEWFXDGNZXEQZXJGBWGGWHYJYKKXJWICGX
      DWJWKWKWLGBWMWN $.
      $([28-Jun-94]$)
  $}

  ${
    $d x A $.
    $( A natural number is either 0 or a successor. $)
    nn0suc $p |- ( A e. om -> ( A = (/) \/ E. x e. om A = suc x ) ) $=
      ( com wcel c0 wceq cv csuc wex wo wrex wn nnsuc exp orrd wa eleq1
      biimpcd peano2b syl6ibr ancrd 19.22dv df-rex imlord mpd ) BCDZBEFZBAGZHZF
      ZAIZJUGUJACKZJUFUGUKUFUGLUKABMNOUFUKULUGUFUKUHCDZUJPZAIULUFUJUNAUFUJUMUFU
      JUICDZUMUJUFUOBUICQRUHSTUAUBUJACUCTUDUE $.
      $([27-May-98]$)
  $}

$(
  ${
    $d x A $.
    findOBS.1 $e |- ( A (_ om /\ (/) e. A /\
                 A. x ( x e. om -> ( x e. A -> suc x e. A ) ) ) $.
$)
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of [TakeutiZaring] p. 43. $)
$(
    findOBS   $p  |- A = om $=
      ( com wss c0 wcel cv csuc wi wal w3a 3simp1 ax-mp peano5 3adant1 eqssi )
      BDBDEZFBGZAHZDGTBGTIBGJJAKZLZRCRSUAMNUBDBEZCSUAUCRABOPNQ $.
$)
      $([4-Aug-94]$)
$(
  $}
$)

  ${
    $d x A $.
    find.1 $e |- ( A (_ om /\ (/) e. A /\ A. x ( x e. A -> suc x e. A ) ) $.
    $( The Principle of Finite Induction (mathematical induction).  Corollary
       7.31 of [TakeutiZaring] p. 43.  The simpler hypothesis shown here was
       suggested in an email from "Colin" on 1-Oct-01.  The hypothesis states
       that ` A ` is a set of natural numbers, zero belongs to ` A ` , and
       given any member of ` A ` the member's successor also belongs to ` A ` .
       The conclusion is that every natural number is in ` A ` . $)
    find   $p |- A = om $=
      ( com wss c0 wcel cv csuc wi wal w3a 3simp1 ax-mp wa ax-1 19.20i imlan
      3anass 3imtr4 peano5 3adant1 eqssi ) BDBDEZFBGZAHZBGUFIBGJZAKZLZUDCUDUEUH
      MNUDUEUFDGZUGJZAKZLZDBEZUIUMCUDUEUHOZOUDUEULOZOUIUMUOUPUDUHULUEUGUKAUGUJP
      QRRUDUEUHSUDUEULSTNUEULUNUDABUAUBNUC $.
      $([1-Dec-01]$)
  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    finds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    finds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    finds.5 $e |- ps $.
    $( Induction hypothesis. $)
    finds.6 $e |- ( y e. om -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136. $)
    finds $p |- ( A e. om -> ta ) $=
      ( com wcel cab c0 cv csuc wi wal wss zfnull elab mpbir visset sucex
      3imtr4g ax-gen peano5 mp2an ssi elabg mpbid ) HOPHAFQZPEOUPHRUPPZGSZOPZUR
      UPPZURTZUPPZUAUAZGUBOUPUCUQBMABFRUDIUEUFVCGUSCDUTVBNACFURGUGZJUEADFVAURVD
      UHKUEUIUJGUPUKULUMAEFHOLUNUO $.
      $([14-Apr-95]$)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    findsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    findsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    findsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    findsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    findsg.5 $e |- ( B e. om -> ps ) $.
    $( Induction hypothesis. $)
    findsg.6 $e |- ( ( ( y e. om /\ B e. om ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the substitutions we
       need.  The last two are the basis and the induction hypothesis.  The
       basis of this version is an arbitrary natural number ` B ` instead of
       zero. $)
    findsg $p |- ( ( ( A e. om /\ B e. om ) /\ B (_ A ) -> ta ) $=
      ( com wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3 pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi
      cleqcoms syl3 com4r con0 onsssuc onelpsst suceloni sylan5 bitrd nnont
      syl2an ancoms ax-1 syl8 a2d com23 sylbird annim syl5ibr pm2.61d finds
      imp31 ) HPQIPQZIHRZEWOIFSZRZATZTWOIUARZBTZTWOIGSZRZCTZTWOIXBUBZRZDTZTWOWP
      ETZTFGHWQUAUCZWSXAWOIUAUCZXIWSXAUDZTXJXIXKXJXIUEWRWTABXIWRWTUDXJWQUAIUFZU
      GXJXIABUDZXJXIWQIUCZXMIUAWQUHJUIUJUKULXJUMZXIXKXIWSWTATXOXAXIWRWTAXLUNXOW
      TABXOWTXMWTXJIUOUPUQURUSULUTVAFGVBZWSXDWOXPWRXCACWQXBIUFKUKVAWQXEUCZWSXGW
      OXQWRXFADWQXEIUFLUKVAWQHUCZWSXHWOXRWRWPAEWQHIUFMUKVAWOBWTNVCXBPQZWOXDXGXS
      WOXDXGTZXSWOUEZXFIXEUCZTZXTWOYCXTTXSYCXDXFWODYCXFWODTZTXDYBYDXFYDXEIXEIUC
      XQXNUEZFVDYDFXEIXBGVEVFVGYEYDFXNWOAXQDXNABWOJNVHXQADLVIVJVKVLVMVNVCVOUGYA
      XFYBUMUEZXTYCUMYAYFXCXTWOXSXCYFUDZIVPQZXBVPQZYGWOXSYHYIUEXCIXEQZYFIXBVQYH
      XEVPQYJYFUDYIIXEVRXBVSVTWAIWBXBWBWCWDYAXDXCXGYAXCCXGYAXCCDXGYAXCCDTOULDXF
      WEWFWGWHWIXFYBWJWKWLULWGWMWN $.
      $([16-Sep-95]$)
  $}

  ${
    $d x y ta $.  $d x ps $.  $d x ch $.  $d x th $. $d y ph $.
    $( Substitutions. $)
    finds2.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    finds2.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    finds2.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    $( Basis. $)
    finds2.4 $e |- ( ta -> ps ) $.
    $( Induction hypothesis. $)
    finds2.5 $e |- ( y e. om -> ( ta -> ( ch -> th ) ) ) $.
    $( Principle of Finite Induction (inference schema) with implicit
       substitutions.  The first three hypotheses establish the substitutions
       we need.  The last two are the basis and the induction hypothesis.
       Theorem Schema 22 of [Suppes] p. 136.  $)
    finds2 $p |- ( x e. om -> ( ta -> ph ) ) $=
      ( cv com wcel wi cab c0 csuc wal wss zfnull wceq bilimd elab mpbir a2d
      visset weq sucex 3imtr4g ax-gen peano5 mp2an ssi abid sylib ) FMZNOUREAPZ
      FQZOUSNUTURRUTOZGMZNOZVBUTOZVBSZUTOZPPZGTNUTUAVAEBPZKUSVHFRUBURRUCABEHUDU
      EUFVGGVCECPZEDPZVDVFVCECDLUGUSVIFVBGUHZFGUIACEIUDUEUSVJFVEVBVKUJURVEUCADE
      JUDUEUKULGUTUMUNUOUSFUPUQ $.
      $([29-Nov-02]$)
  $}

  ${
    $d x y z $.  $d y z ph $.
    findes.1 $e |- [ (/) / x ] ph $.
    findes.2 $e |- ( x e. om -> ( ph -> [ suc x / x ] ph ) ) $.
    $( Finite induction with explicit substitution.  The first hypothesis is
       the basis, and the second is the induction hypothesis.  Theorem Schema
       22 of [Suppes] p. 136. (Contributed by Raph Levien 9-Jul-03.) $)
    findes   $p |- ( x e. om -> ph ) $=
      ( vz vy wsb c0 wsbc cv csuc dfsbcq sbequ weq sbid syl6bb com wcel wi
      ax-17 hbs1 visset sucex hbsbcv hbim eleq1 sbequ12 wceq wb suceq syl
      bi2imd chv2 finds ) ABEGZABHIABFGZABFJZKZIZAEFBJZABEJZHLAEFBMABVAURLEBNUO
      ABBGAAEBBMABOPCUTQRZAABUTKZIZSZSUQQRZUPUSSZSBFVFVGBVFBTUPUSBABFUAABURUQFU
      BUCUDUEUEBFNZVBVFVEVGUTUQQUFVHAUPVDUSABFUGVHVCURUHVDUSUIUTUQUJABVCURLUKUL
      ULDUMUN $.
      $([29-Jul-03]$)
  $}

  ${
    $d x y z $.  $d x A $.  $d x ps $.  $d x z ch $.  $d x th $.  $d x ta $.
    $d y z ph $.  $( ` z ` is dummy. $)
    $( Substitutions. $)
    tfinds.1 $e |- ( x = (/) -> ( ph <-> ps ) ) $.
    tfinds.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfinds.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfinds.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfinds.5 $e |- ps $.
    $( Induction hypothesis for successors. $)
    tfinds.6 $e |- ( y e. On -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfinds.7 $e |- ( Lim x -> ( A. y ( y e. x -> ch ) -> ph ) ) $.
    $( Principle of Transfinite Induction (inference schema) with implicit
       substitutions.  The first four hypotheses establish the
       substitutions we need.  The last three are the basis, the induction
       hypothesis for successors, and the induction hypothesis for limit
       ordinals.  Theorem Schema 4 of [Suppes] p. 197. $)
    tfinds $p |- ( A e. On -> ta ) $=
      ( vz con0 cv wcel wlim wel wi wal wn c0 wceq csuc wex wo word eloni cuni
      w3a df-lim biimpr 3exp pm2.16 syl6 com23 orduninsuc biimprd pm2.15d
      syl6d df-or syl6ibr syl mpbiri a1d ax-17 hba1 hbim sucelon visset sucid
      weq eleq1 bi2imd a4b1 mpi syl5 sylbir wsb eleq2 birimd bialdv a13b sbequ
      ddel4 syl5bbr cbvalv hbs1 sbequ12 cbval 3bitr4g 19.23ai jaoi pm2.43a
      pm2.61d2 tfis2 vtoclga ) AEFHQLACFGJFRZQSZXATZGFUAZCUBZGUCZAUBZXCUDZXBXGX
      BXHXAUEUFZXAGRZUGZUFZGUHZUIZXBXGUBZXBXAUJZXHXNUBXAUKXPXHXIUDZXMUBXNXPXHXQ
      XAXAULUFZUDZXMXPXQXHXSXPXQXRXCUBXHXSUBXPXQXRXCXCXPXQXRUMXAUNUOUPXRXCUQURU
      SXPXMXRXPXRXMUDGXAUTVAVBVCXIXMVDVEVFXIXOXMXIXGXBXIAXFXIABMIVGVHVHXLXOGXBX
      GGXBGVIXFAGXEGVJAGVIVKVKXLXOXKQSZXAXKSZAUBZFUCZDUBZUBXTXJQSZYDXJVLYECDYCN
      YCXJXKSZCXJGVMVNYBYFCUBFGFGVOYAYFACXAXJXKVPJVQVRVSVTWAXLXBXTXGYDXAXKQVPXL
      XFYCADXLPFUAZAFPWBZUBZPUCPRZXKSZYHUBZPUCXFYCXLYIYLPXLYGYKYHXAXKYJWCWDWEXE
      YIGPGPVOZXDYGCYHGPFWFYMAFGWBYHCAGPFWGACFGCFVIJWHWIVQWJYBYLFPYBPVIYKYHFYKF
      VIAFPWKVKFPVOYAYKAYHXAYJXKVPAFPWLVQWMWNKVQVQVGWOWPURWQOWRWSWT $.
      $([16-Apr-95]$)
  $}

  ${
    $d x A $.  $d x y B $.
    $d x ps $.  $d x ch $.  $d x th $.  $d x ta $. $d y ph $.
    $( Substitutions. $)
    tfindsg.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    tfindsg.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    tfindsg.3 $e |- ( x = suc y -> ( ph <-> th ) ) $.
    tfindsg.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    tfindsg.5 $e |- ( B e. On -> ps ) $.
    $( Induction hypothesis for successor ordinals. $)
    tfindsg.6 $e |- ( ( ( y e. On /\ B e. On ) /\ B (_ y ) -> ( ch -> th ) ) $.
    $( Induction hypothesis for limit ordinals. $)
    tfindsg.7 $e |- ( ( ( Lim x /\ B e. On ) /\ B (_ x ) ->
                   ( A. y ( ( y e. x /\ B (_ y ) -> ch ) -> ph ) ) $.
    $( Transfinite Induction (inference schema) with implicit substitutions.
       The first four hypotheses establish the substitutions we need.  The last
       three are the basis, the induction hypothesis for successors, and the
       induction hypothesis for limit ordinals.  The basis of this version is
       an arbitrary ordinal ` B ` instead of zero. $)
    tfindsg $p |- ( ( ( A e. On /\ B e. On ) /\ B (_ A ) -> ta ) $=
      ( con0 wcel wss cv wi c0 csuc wceq wb wa sseq2 adantl cleq2 syl6bir imp
      bi2imd exp wn birimd ss0 con3 pm2.21d pm5.74d sylan9bbr pm2.61i bilimd
      weq a1d wex visset sucex eqvinc syl5bir biimpd sylan9r 19.23aiv sylbi
      cleqcoms syl3 com4r onsssuc onelpsst suceloni sylan5 bitrd ancoms ax-1
      syl8 a2d com23 sylbird annim syl5ibr pm2.61d wlim wel wal pm2.27 syl3d
      imp4a 19.20dv ad2antlr syld exp31 com3l com4t tfinds imp31 ) HQRIQRZIHSZE
      XEIFTZSZAUAZUAXEIUBSZBUAZUAXEIGTZSZCUAZUAZXEIXLUCZSZDUAZUAXEXFEUAZUAFGHXG
      UBUDZXIXKXEIUBUDZXTXIXKUEZUAYAXTYBYAXTUFXHXJABXTXHXJUEYAXGUBIUGZUHYAXTABU
      EZYAXTXGIUDZYDIUBXGUIJUJUKULUMYAUNZXTYBXTXIXJAUAYFXKXTXHXJAYCUOYFXJABYFXJ
      YDXJYAIUPUQURUSUTUMVAVBFGVCZXIXNXEYGXHXMACXGXLIUGKULVBXGXPUDZXIXRXEYHXHXQ
      ADXGXPIUGLULVBXGHUDZXIXSXEYIXHXFAEXGHIUGMULVBXEBXJNVDXLQRZXEXNXRYJXEXNXRU
      AZYJXEUFZXQIXPUDZUAZYKXEYNYKUAYJYNXNXQXEDYNXQXEDUAZUAXNYMYOXQYOXPIXPIUDYH
      YEUFZFVEYOFXPIXLGVFVGVHYPYOFYEXEAYHDYEABXEJNVIYHADLVJVKVLVMVNVOVDVPUHYLXQ
      YMUNUFZYKYNUNYLYQXMYKXEYJXMYQUEXEYJUFXMIXPRZYQIXLVQXEXPQRYRYQUEYJIXPVRXLV
      SVTWAWBYLXNXMXRYLXMCXRYLXMCDXRYLXMCDUAOUMDXQWCWDWEWFWGXQYMWHWIWJUMWEXEXHX
      GWKZGFWLZXOUAZGWMZAYSXEXHUVBAUAZYSXEXHUVCYSXEUFXHUFUVBYTXMUFCUAZGWMZAXEUV
      BUVEUAYSXHXEUVAUVDGXEUVAYTXMCXEXOXNYTXEXNWNWOWPWQWRPWSWTXAXBXCXD $.
      $([4-Sep-03]$)
  $}

  ${
    $d x y z $.  $d y z ph $.
    tfindes.1 $e |- [ (/) / x ] ph $.
    tfindes.2 $e |- ( x e. On -> ( ph -> [ suc x / x ] ph ) ) $.
    tfindes.3 $e |- ( Lim y -> ( A. x ( x e. y -> ph ) -> [ y / x ] ph ) ) $.
    $( Transfinite induction with explicit substitution.  The first hypothesis
       is the basis, the second is the induction hypothesis for successors, and
       the third is the induction hypothesis for limit ordinals.    Theorem
       Schema 4 of [Suppes] p. 197. $)
    tfindes   $p |- ( x e. On -> ph ) $=
      ( vz wsb c0 wsbc cv csuc dfsbcq sbequ weq sbid syl6bb con0 wcel wi ax-17
      hbs1 visset sucex hbsbcv hbim eleq1 sbequ12 wceq wb suceq syl bi2imd
      chv2 wlim wel wal a13b cbval syl5ibr tfinds ) ABCHZABIJABGHZABGKZLZJZACGB
      KZABCKZIMACGBNABVHVEMCBOVBABBHAACBBNABPQDVGRSZAABVGLZJZTZTVDRSZVCVFTZTBGV
      MVNBVMBUAVCVFBABGUBZABVEVDGUCUDUEUFUFBGOZVIVMVLVNVGVDRUGVPAVCVKVFABGUHZVP
      VJVEUIVKVFUJVGVDUKABVJVEMULUMUMEUNVHUOBCUPZATZBUQVBGCUPZVCTZGUQFVSWABGVSG
      UAVTVCBVTBUAVOUFVPVRVTAVCBGCURVQUMUSUTVA $.
      $([29-Jul-03]$)
  $}

