$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The mapping operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbol. $)
  $c ^m $.  $( The mapping operation. $)

  $( Extend the definition of a class to include the mapping operation.
     (Read for ` A ^m B ` , "the set of all functions that map from
     ` B ` to ` A ` .) $)
  cm $a class ^m $.

  ${
    $d x y z f $.
    $( Define the mapping operation or set exponentiation.  The set of all
       functions that map from ` B ` to ` A ` is written ` ( A ^m B ) ` (see
       ~ mapval ).  Many authors write ` A ` followed by ` B ` as a superscript
       for this operation and rely on context to avoid confusion other
       exponentiation operations (e.g. Definition 10.42 of [TakeutiZaring]
       p. 95).  Other authors show ` B ` as a prefixed superscript, which is
       read " ` A ` pre ` B ` " (e.g. definition of [Enderton] p. 52).
       Definition 8.21 of [Eisenberg] p. 125 uses the notation Map( ` B ` ,
       ` A ` ) for our ` ( A ^m B ) ` .  The up-arrow is used by Donald Knuth
       for iterated exponentiation (_Science_ 194, 1235-1242, 1976).  We adopt
       the first case of his notation (simple exponentiation) and subscript it
       with _m_ to distinguish it from other kinds of exponentiation. $)
    df-map $a |- ^m = { <. <. x , y >. , z >. | z = { f | f : y --> x } } $.
  $}

  ${
    $d f x A $.  $d f x B $.
    $( When ` A ` is a proper class, the class of all functions mapping ` A `
       to ` B ` is empty.  Exercise 4.41 of [Mendelson] p. 255. $)
    mapprc $p |- ( -. A e. V -> { f | f : A --> B } = (/) ) $=
      ( cv wf cab c0 wceq cvv wcel wn wex noel3 cdm visset dmexg ax-mp fdm
      eleq1d mpbii 19.23aiv sylbi con1i ) ABCDZEZCFGHZAIJZUFKUECLUGUECMUEUGCUEU
      DNZIJZUGUDIJUICOUDIPQUEUHAIABUDRSTUAUBUC $.
      $( [10-Dec-03] $) $( [8-Dec-03] $)

    $( The class of all functions mapping one set to another is a set.  Remark
       after Definition 10.24 of [Kunen] p. 31.   (Contributed by Raph Levien,
       4-Dec-03.) $)
    mapex $p |- ( ( A e. C /\ B e. D ) -> { f | f : A --> B } e. V ) $=
      ( wcel wa cv wf cab cxp cpw wss cvv fssxp ss2abi df-pw sseqtr4 wi xpexg
      pwexg ssexg 3syl mpi ) ACFBDFGZABEHZIZEJZABKZLZMZUHNFZUHUFUIMZEJUJUGUMEAB
      UFOPEUIQRUEUINFUJNFUKULSABCDTUINUAUHUJNUBUCUD $.
      $( [8-Dec-03] $) $( [8-Dec-03] $)
  $}

  ${
    $d f x y z $.
    $( Set exponentiation has a universal domain. $)
    fnmap $p |- ^m Fn ( V X. V ) $=
      ( vx vy vz vf cvv cv wf cab cm wcel visset mapex mp2an wceq copab2 wa
      df-map pm3.2i biantrur bioprab eqtr fnoprab2 ) ABCEEBFZAFZDFGDHZIUCEJZUDE
      JZUEEJBKZAKZUCUDEEDLMICFUENZABCOUGUFPZUJPZABCOABCDQUJULABCUKUJUGUFUIUHRST
      UAUB $.
      $( [16-Jun-04] $) $( [8-Dec-03] $)
  $}

  ${
    $d x y z f A $. $d x y z f B $.
    $( The value of set exponentiation.  ` ( A ^m B ) ` is the set of all
       functions that map from ` B ` to ` A ` .  Definition 10.24 of [Kunen]
       p. 24. $)
    mapvalg $p |- ( ( A e. C /\ B e. D ) ->
                  ( A ^m B ) = { f | f : B --> A } ) $=
      ( vx vy vz wcel wa cv wf cab cvv cm co wceq mapex ancoms wi feq3 biabdv
      feq2 copab2 df-map visset pm3.2i biantrur bioprab eqtr oprabval2g 3exp
      imp elisset syl2an mpd ) ACIZBDIZJBAEKZLZEMZNIZABOPVAQZURUQVBBADCERSANIZB
      NIZVBVCTZUQURVDVEVFVDVEVBVCFGHABNNGKZFKZUSLZEMZVAOVGAUSLZEMNVHAQVIVKEVHAV
      GUSUAUBVGBQVKUTEVGBAUSUCUBOHKVJQZFGHUDVHNIZVGNIZJZVLJZFGHUDFGHEUEVLVPFGHV
      OVLVMVNFUFGUFUGUHUIUJUKULUMACUNBDUNUOUP $.
      $( [8-Dec-03] $) $( [8-Dec-03] $)
  $}

  ${
    $d f A $.  $d f B $.
    mapval.1 $e |- A e. V $.
    mapval.2 $e |- B e. V $.
    $( The value of set exponentiation (inference version).  ` ( A ^m B ) `
       is the set of all functions that map from ` B ` to ` A ` .  Definition
       10.24 of [Kunen] p. 24. $)
    mapval $p |-  ( A ^m B ) = { f | f : B --> A } $=
      ( cvv wcel cm co cv wf cab wceq mapvalg mp2an ) AFGBFGABHIBACJKCLMDEABFFC
      NO $.
      $( [8-Dec-03] $) $( [8-Dec-03] $)
  $}

  ${
    $d f A $.  $d f B $.  $d f C $.
    elmap.1 $e |- A e. V $.
    elmap.2 $e |- B e. V $.
    $( Membership relation for set exponentiation. $)
    elmap $p |-  ( C e. ( A ^m B ) <-> C : B --> A ) $=
      ( vf cm co wcel cv wf cab mapval eleq2i cvv wi fex ax-mp feq1 elab3g
      bitr ) CABGHZICBAFJZKZFLZIBACKZUBUECABFDEMNUDUFFCBOIUFCOIPEBAOCQRBAUCCSTU
      A $.
      $( [30-Jan-04] $) $( [8-Dec-03] $)
  $}

  ${
    $d f A $.
    map0e.1 $e |- A e. V $.
    $( Set exponentiation with an empty exponent (ordinal number 0) is ordinal
       number 1.  Exercise 4.42(a) of [Mendelson] p. 255. $)
    map0e $p |- ( A ^m (/) ) = 1o $=
      ( vf c0 cm co csn c1o cv wf cab wceq wfn crn wss wa fn0 biran df-f 0ss
      rneq rn0 syl6eq sseq1d mpbiri pm4.71i 3bitr4 biabi 0ex mapval df-sn
      3eqtr4 df1o2 eqtr4 ) ADEFZDGZHDACIZJZCKUQDLZCKUOUPURUSCUQDMZUQNZAOZPUSVBP
      URUSUTUSVBUQQRDAUQSUSVBUSVBDAOATUSVADAUSVADNDUQDUAUBUCUDUEUFUGUHADCBUIUJC
      DUKULUMUN $.
      $( [10-Dec-03] $) $( [10-Dec-03] $)

    $( Set exponentiation with an empty base is the empty set, provided the
       exponent is non-empty.  Theorem 96 of [Suppes] p. 89. $)
    map0b $p |- ( -. A = (/) -> ( (/) ^m A ) = (/) ) $=
      ( vf c0 wceq wn cv wf cab cm co wex noel3 cdm fdm crn wss frn ss0 syl
      dm0rn0 sylibr eqtr3d 19.23aiv sylbi con1i 0ex mapval syl5eq ) ADEZFADCGZH
      ZCIZDDAJKUMDEZUJUNFULCLUJULCMULUJCULUKNZADADUKOULUKPZDEZUODEULUPDQUQADUKR
      UPSTUKUAUBUCUDUEUFDACUGBUHUI $.
      $( [15-Dec-03] $) $( [10-Dec-03] $)
  $}

  ${
    $d f x y A $. $d f x y B $.
    map0.1 $e |- A e. V $.
    map0.2 $e |- B e. V $.
    $( Set exponentiation is empty iff the base is empty and the exponent is
       not empty. Theorem 97 of [Suppes] p. 89. $)
    map0 $p |- ( ( A ^m B ) = (/) <-> ( A = (/) /\ -. B = (/) ) ) $=
      ( vf vx cm co c0 wceq wn wa cv wf cab mapval cleq1i wcel wex csn wss cxp
      snssi visset fconst fss mpan snex xpex feq1 cla4ev 3syl 19.23aiv n0
      noel3 3imtr4 a3i sylbi 0nep0 c1o map0e df1o2 cleqcom 3bitr mtbir opreq2
      cleq1d mtbiri con2i jca opreq1 map0b sylan9eq impbi ) ABGHZIJZAIJZBIJZKZL
      VPVQVSVPBAEMZNZEOZIJZVQVOWBIABECDPQVQWCFMZARZFSWAESZVQKWCKWEWFFWEWDTZAUAZ
      BABWGUBZNZWFWDAUCBWGWINWHWJBWDFUDUEBWGAWIUFUGWAWJEWIBWGDWDUHUIBAVTWIUJUKU
      LUMFAUNWAEUOUPUQURVRVPVRVPAIGHZIJZWLIITZJZUSWLUTIJWMIJWNWKUTIACVAQUTWMIVB
      QWMIVCVDVEVRVOWKIBIAGVFVGVHVIVJVQVSVOIBGHIAIBGVKBDVLVMVN $.
      $( [15-Dec-03] $) $( [10-Dec-03] $)

    $( The value of set exponentiation with a singleton exponent.  Theorem 98
       of [Suppes] p. 89. $)
    mapsn $p |- ( A ^m { B } ) = { f | E. y e. A f = { <. B , y >. } } $=
      ( cv cop csn wceq wrex cm co wcel wf snex elmap wa wex crn weu snid wfn
      fneu2 ffn sylan mpan2 cab cima wrel frel imasn syl cdm fdm imaeq2
      imadmrn syl5reqr eqtr3d cleq1d biexdv eusn syl5bb mpbid frn sseld visset
      eleq2 mpbiri syl5 feq3 wfo fnforn sylib fof syl5bi com12 fsn syl6ib jcad
      19.22dv mpd df-rex sylibr wss fss wf1o f1osn f1oeq1 f1of snssi syl2an
      exp r19.23aiv impbi bitr biabri ) DGZCAGZHZIZJZABKZDBCIZLMZWRXENXDBWROZXC
      BXDWRECPQXFXCXFWSBNZXBRZASZXCXFWRTZWSIZJZASZXIXFWTWRNZAUAZXMXFCXDNZXOCFUB
      WRXDUCZXPXOXFAXDCWRUDXDBWRUEZUFUGXFXNAUHZXKJZASXMXOXFXTXLAXFXSXJXKXFWRXDU
      IZXSXJXFWRUJYAXSJXDBWRUKACWRULUMXFWRWRUNZUIZYAXJXFYBXDJYCYAJXDBWRUOYBXDWR
      UPUMWRUQURUSUTVAXNAVBVCVDXFXLXHAXFXLXGXBXFWSXJNZXGXLXFXJBWSXDBWRVEVFXLYDW
      SXKNWSAVGZUBXJXKWSVHVIVJXFXLXDXKWROZXBXLXFYFXLXDXJWROZYFXFXJXKXDWRVKXFXDX
      JWRVLZYGXFXQYHXRXDWRVMVNXDXJWRVOUMVPVQCWSWRFYEVRVSVTWAWBXBABWCWDXBXFABXBX
      GXFXBXGXFYFXKBWEXFXBXGXDXKBWRWFXBXDXKWRWGZYFXBYIXDXKXAWGCWSFYEWHXDXKWRXAW
      IVIXDXKWRWJUMWSBWKWLWMVQWNWOWPWQ $.
      $( [15-Dec-03] $) $( [10-Dec-03] $)
  $}

  ${
    $d f A $. $d f B $.  $d f C $.
    mapss.1 $e |- B e. V $.
    mapss.2 $e |- C e. V $.
    $( Subset inheritance for set exponentiation.  Theorem 99 of [Suppes]
       p. 89. $)
    mapss $p |- ( A (_ B -> ( A ^m C ) (_ ( B ^m C ) ) $=
      ( vf wss cv wf cab cm co wi wal fss exp com12 19.21aiv ss2ab sylibr cvv
      wcel wceq ssex mapvalg mpan2 syl mapval a1i 3sstr4d ) ABGZCAFHZIZFJZCBULI
      ZFJZACKLZBCKLZUKUMUOMZFNUNUPGUKUSFUMUKUOUMUKUOCABULOPQRUMUOFSTUKAUAUBZUQU
      NUCZABDUDUTCUAUBVAEACUAUAFUEUFUGURUPUCUKBCFDEUHUIUJ $.
      $( [15-Dec-03] $) $( [10-Dec-03] $)
  $}


