$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The empty set; derive the Axiom of the Empty Set
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the empty or null set. $)
  $c (/) $. $( null set $)

  $( Extend class notation to include the empty set. $)
  c0  $a class (/) $.

  $( Define the empty set.  For a more "typical" definition, but requiring
     a dummy variable, see ~ dfnul2 . $)
  df-nul $a |- (/) = ( V \ V ) $.

  $( Alternate definition of the empty set.  Definition 5.14 of Takeuti and
     Zaring, p. 20. $)
  dfnul2 $p |- (/) = { x | -. x = x } $=
    ( weq wn c0 cv wcel cvv cdif wa df-nul eleq2i eldif eqid pm3.24 2th bicon2
    3bitr cleqabri ) AABZCZADAEZDFUAGGHZFUAGFZUCCIZTDUBUAJKUAGGLSUDSUDCAMUCNOPQ
    R $.
    $([26-Dec-96]$)

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( An empty set has no elements.  Theorem 6.14 of Quine, p. 44. $)
    noel $p |- -. A e. (/) $=
      ( vx cvv wcel c0 wn cv wceq weq eqid dfnul2 cleqabi bicon2 mpbi eleq1
      mtbii vtocle elisset con3 pm2.61i ) ACDZAEDZFZUCBABGZAHUDEDZUBBBIZUEFBJUE
      UFUFFBEBKLMNUDAEOPQUBUAAERST $.
      $([5-Aug-93]$)
  $}

  $( If a set has elements, it is not empty. $)
  noel2 $p |- ( B e. A -> -. A = (/) ) $=
    ( c0 wceq wcel noel eleq2 mtbiri con2 ) ACDZBAEZJKBCEBFACBGHI $.
    $([31-Dec-93]$)

  ${
    $d x A $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       Takeuti and Zaring, p. 20. $)
    nnull $p |- ( -. A = (/) <-> E. x x e. A ) $=
      ( c0 wceq wn cv wcel wal wex wb dfcleq noel nbn bial bitr4 bineg df-ex )
      BCDZEAFZBGZEZAHZETAIRUBRTSCGZJZAHUBABCKUAUDAUCTSLMNOPTAQO $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Non-empty class abstraction. $)
    noel3 $p |- ( -. { x | ph } = (/) <-> E. x ph ) $=
      ( vy cv cab wcel wex c0 wceq wn hbab1 ax-17 eleq1 cbvex nnull abid biex
      3bitr4 ) CDZABEZFZCGBDZTFZBGTHIJABGUAUCCBABCKUCCLSUBTMNCTOAUCBABPQR $.
      $([26-Dec-96]$)
  $}

  $( Non-empty restricted class abstraction. $)
  rnoel3 $p |- ( -. { x e. A | ph } = (/) <-> E. x e. A ph ) $=
    ( cv wcel wa cab c0 wceq wn wex crab wrex noel3 df-rab cleq1i bineg df-rex
    3bitr4 ) BDCEAFZBGZHIZJTBKABCLZHIZJABCMTBNUDUBUCUAHABCOPQABCRS $.
    $([29-Aug-99]$)

  ${
    $d x A $.
    $( An empty set has no elements.  Theorem 2, p. 22, of Suppes. $)
    nulleq $p |- ( A = (/) <-> A. x -. x e. A ) $=
      ( c0 wceq cv wcel wn wal wex nnull df-ex bitr bicon4 ) BCDZAEBFZGAHZNGOAI
      PGABJOAKLM $.
      $([29-Aug-93]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The union of a class with the empty set is itself.  Theorem 24 of
       Suppes, p. 27. $)
    un0 $p |- A = ( A u. (/) ) $=
      ( vx c0 cv wcel noel biorfi uneqri ) BAACBDZCEIAEIFGH $.
      $([5-Aug-93]$)

    $( The union of a class with the universal class is the universal class. $)
    unv $p |- V = ( A u. V ) $=
      ( vx cvv cv wcel wi wo wb elisset pm4.72 mpbi uneqri ) BCACBDZAEZMCEZFONO
      GHMAINOJKL $.
      $([17-May-98]$)

    $( The intersection of a class with the empty set is the empty set.
       Theorem 16 of Suppes, p. 26. $)
    in0 $p |- (/) = ( A i^i (/) ) $=
      ( vx c0 cv wcel noel bianfi ineqri ) BCACBDZCEIAEIFGH $.
      $([5-Aug-93]$)

    $( The intersection of a class with the universal class is itself. $)
    inv $p |- A = ( A i^i V ) $=
      ( vx cvv cv wcel visset biantru ineqri ) BAACBDZCEIAEBFGH $.
      $([17-May-98]$)
  $}

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( The null set is a subset of any class.  Part of Exercise 1 of Takeuti
       and Zaring, p. 22. $)
    nullss $p |- (/) (_ A $=
      ( vx c0 cv wcel noel pm2.21i ssriv ) BCABDZCEIAEIFGH $.
      $([5-Aug-93]$)

    $( Any class is a subclass of the universal class. $)
    ssv $p |- A (_ V $=
      ( vx cvv cv elisset ssriv ) BACBDAEF $.
      $([31-Oct-95]$)
  $}

  $( Any subset of the empty set is empty.  Theorem 5 of Suppes, p. 23. $)
  ss0 $p |- ( A (_ (/) -> A = (/) ) $=
    ( c0 wss wceq nullss wa eqss biimpr mpan2 ) ABCZBACZABDZAELJKFABGHI $.
    $([13-Aug-94]$)

  $( Only the universal class has the universal class as a subclass. $)
  vss $p |- ( V (_ A -> A = V ) $=
    ( cvv wss wa wceq ssv jctl eqss sylibr ) BACZABCZJDABEJKAFGABHI $.
    $([10-May-98]$)

  $( The null set is a proper subset of any non-empty set. $)
  nullpss $p |- ( (/) (. A <-> -. A = (/) ) $=
    ( c0 wpss wceq wn wss dfpss2 nullss mpbiran cleqcom bineg bitr ) BACZBADZEZ
    ABDZEMBAFOBAGAHINPBAJKL $.
    $([27-Feb-96]$)

  $( No set is a proper subset of the empty set. $)
  npss0 $p |- -. A (. (/) $=
    ( c0 wpss wceq cleqid wn wss wb pssss ss0 psseq1 3syl biimpd pm2.43i
    nullpss sylib mt2 ) ABCZBBDZBERBBCZSFRTRRTRABGABDRTHABIAJABBKLMNBOPQ $.
    $([17-Jun-98]$)

  $( Any non-universal class is a proper subclass of the universal class. $)
  pssv $p |- ( A (. V <-> -. A = V ) $=
    ( cvv wpss wss wceq wn dfpss2 ssv mpbiran ) ABCABDABEFABGAHI $.
    $([17-May-98]$)

  ${
    $d A x $.  $d B x $.
    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj $p |- ( ( A i^i B ) = (/) <-> A. x ( x e. A -> -. x e. B ) ) $=
      ( cin c0 wceq cv wcel wa cab wb wal wn wi dfin2 cleq1i cleqrab imnan
      noel nbn bitr2 bial 3bitr ) BCDZEFAGZBHZUECHZIZAJZEFUHUEEHZKZALUFUGMNZALU
      DUIEABCOPUHAEQUKULAULUHMUKUFUGRUJUHUESTUAUBUC $.
      $([19-Aug-93]$)

    $( Two ways of saying that two classes are disjoint. $)
    disj2 $p |- ( ( A i^i B ) = (/) <-> A (_ ( V \ B ) ) $=
      ( vx cv wcel wn wi wal cvv cdif cin c0 wceq wss wa visset biantrur eldif
      bitr4 bilim bial disj df-ss 3bitr4 ) CDZAEZUEBEFZGZCHUFUEIBJZEZGZCHABKLMA
      UINUHUKCUGUJUFUGUEIEZUGOUJULUGCPQUEIBRSTUACABUBCAUIUCUD $.
      $([17-May-98]$)

    $( Two ways of saying that two classes are disjoint. $)
    disj3 $p |- ( ( A i^i B ) = (/) <-> A = ( A \ B ) ) $=
      ( vx cv wcel wn wi wal cdif wb cin c0 wceq wa pm4.71 eldif bilbi bitr4
      bial disj dfcleq 3bitr4 ) CDZAEZUCBEFZGZCHUDUCABIZEZJZCHABKLMAUGMUFUICUFU
      DUDUENZJUIUDUEOUHUJUDUCABPQRSCABTCAUGUAUB $.
      $([19-May-98]$)
  $}

  $( The intersection of classes with a common member is nonempty. $)
  inelcm $p |- ( ( A e. B /\ A e. C ) -> -. ( B i^i C ) = (/) ) $=
    ( wcel wa cin c0 wceq wn elin noel2 sylbir ) ABDACDEABCFZDMGHIABCJMAKL $.
    $([7-Apr-94]$)

  $( A minimum element of a class has no elements in common with the class. $)
  minel $p |- ( ( A e. B /\ ( C i^i B ) = (/) ) -> -. A e. C ) $=
    ( wcel cin c0 wceq wn wa wi inelcm con2 imnan sylibr pm2.03d comm imp ) ABD
    ZCBEFGZACDZHZSRUASTRSTRIZHTRHJUBSACBKLTRMNOPQ $.
    $([22-Jun-94]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Distribute union over difference. $)
    undif4 $p |- ( ( A i^i C ) = (/) ->
                 ( A u. ( B \ C ) ) = ( ( A u. B ) \ C ) ) $=
      ( vx cv wcel wn wi wal cdif cun wb cin c0 wceq wo wa pm2.61 ax-1 a1i
      impbid df-or syl5bb biland eldif bilor ordi bitr elun biran 3bitr4g
      19.20i disj dfcleq 3imtr4 ) DEZAFZUPCFGZHZDIUPABCJZKZFZUPABKZCJZFZLZDIACM
      NOVAVDOUSVFDUSUQUPUTFZPZUPVCFZURQZVBVEUSUQUPBFZPZUQURPZQZVLURQVHVJUSVMURV
      LUSUQGZURHZURVMUSVPURUQURRURVPHUSURVOSTUAUQURUBUCUDVHUQVKURQZPVNVGVQUQUPB
      CUEUFUQVKURUGUHVIVLURUPABUIUJUKUPAUTUIUPVCCUEUKULDACUMDVAVDUNUO $.
      $([17-May-98]$)
  $}

  $( ` x ` is a dummy variable in the proof below. $)
  $( The Null Set axiom of ZF set theory:  the empty class exists.  Corollary
     5.16 of Takeuti and Zaring, p. 20. $)
  zfnull $p |- (/) e. V $=
    ( vx c0 cv cin cvv in0 visset zfauscl eqeltr ) BACZBDEJFJBAGHI $.
    $([5-Aug-93]$)

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass expressed in terms of difference.  Exercise 7 of Takeuti and
       Zaring, p. 22. $)
    ssdif0 $p |- ( A (_ B <-> ( A \ B ) = (/) ) $=
      ( vx cv wcel wi wal cdif wn wss c0 wceq wa iman eldif bineg bitr4 bial
      df-ss nulleq 3bitr4 ) CDZAEZUBBEZFZCGUBABHZEZIZCGABJUFKLUEUHCUEUCUDIMZIUH
      UCUDNUGUIUBABOPQRCABSCUFTUA $.
      $([29-Apr-94]$)
  $}

  $( A proper subclass has a nonempty difference. $)
  pssdifn0 $p |- ( ( A (_ B /\ -. A = B ) -> -. ( B \ A ) = (/) ) $=
    ( wss wceq wn cdif c0 wa eqss biimpr exp ssdif0 syl5ibr pm2.16d imp ) ABCZA
    BDZEBAFGDZEPRQPBACZQRPSQQPSHABIJKBALMNO $.
    $([3-May-94]$)

  ${
    $d x A $.  $d x B $.
    $( A proper subclass has a member in one argument that's not in both. $)
    pssnel $p |- ( A (. B -> E. x ( x e. B /\ -. x e. A ) ) $=
      ( wpss cv cdif wcel wex wn wa c0 wceq wss dfpss2 pssdifn0 sylbi nnull
      sylib eldif biex ) BCDZAEZCBFZGZAHZUBCGUBBGIJZAHUAUCKLIZUEUABCMBCLIJUGBCN
      BCOPAUCQRUDUFAUBCBSTR $.
      $([29-Feb-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Difference, intersection, and subclass relationship. $)
    difin0ss $p |- ( ( ( A \ B ) i^i C ) = (/) -> ( C (_ A -> C (_ B ) ) $=
      ( vx cdif cin c0 wceq cv wcel wi wal wss wn nulleq wa annim bilan ancom
      bitr3 bineg iman elin eldif biran bitr 3bitr4 ax-2 sylbir 19.20ii sylbi
      df-ss 3imtr4g ) ABEZCFZGHZDIZCJZUQAJZKZDLZURUQBJZKZDLZCAMCBMUPUQUOJZNZDLV
      AVDKDUOOVFUTVCDVFURUSVBKZKZUTVCKURVGNZPZNUSVBNPZURPZNVHVFVJVLVJURVKPVLVKV
      IURUSVBQRURVKSTUAURVGUBVEVLVEUQUNJZURPVLUQUNCUCVMVKURUQABUDUEUFUAUGURUSVB
      UHUIUJUKDCAULDCBULUM $.
      $([30-Apr-94]$)

    $( Intersection, subclass, and difference relationship. $)
    inssdif0 $p |- ( ( A i^i B ) (_ C <-> ( A i^i ( B \ C ) ) = (/) ) $=
      ( vx cv cin wcel wi wal cdif wn wss c0 wceq wa impexp iman bilim imnan
      3bitr elin birim eldif bilan bitr bineg 3bitr4 bial df-ss nulleq ) DEZABF
      ZGZUKCGZHZDIUKABCJZFZGZKZDIULCLUQMNUOUSDUKAGZUKBGZOZUNHZUTVAUNKOZOZKZUOUS
      VCUTVAUNHZHUTVDKZHVFUTVAUNPVGVHUTVAUNQRUTVDSTUMVBUNUKABUAUBURVEURUTUKUPGZ
      OVEUKAUPUAVIVDUTUKBCUCUDUEUFUGUHDULCUIDUQUJUG $.
      $([27-Oct-96]$)
  $}

  $( The intersection of a class and its complement is empty. $)
  indif0 $p |- ( A i^i ( B \ A ) ) = (/) $=
    ( cin wss cdif c0 wceq inss1 inssdif0 mpbi ) ABCADABAECFGABHABAIJ $.
    $([24-Mar-98]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The difference between a class and itself is the empty set.
       Generalization of Proposition 5.15, p. 20, of Takeuti and Zaring. $)
    difid $p |- (/) = ( A \ A ) $=
      ( vx weq wn cab cv wcel wa c0 cdif pm3.24 eqid 2th bicon1 biab dfnul2
      df-dif 3eqtr4 ) BBCZDZBEBFAGZUADHZBEIAAJTUBBUBSUBDSUAKBLMNOBPBAAQR $.
      $([16-Sep-93]$)
  $}

  ${
    $d x A $.  $d x ph $.
    $( Restricted quantification of wff not containing quantified variable. $)
    r19.3rzv $p |- ( -. A = (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wceq wn cv wcel wex wi wral wb nnull biimt sylbi wal df-ral 19.23v
      bitr syl6bbr ) CDEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABRS
      T $.
      $([10-Mar-97]$)

    $( Restricted quantification of wff not containing quantified variable. $)
    r19.9rzv $p |- ( -. A = (/) -> ( ph <-> E. x e. A ph ) ) $=
      ( c0 wceq wn wral wrex r19.3rzv bicomd bicon2d dfrex2 syl6bbr ) CDEFZAAFZ
      BCGZFABCHNPANOPOBCIJKABCLM $.
      $([27-May-98]$)

    $( Restricted version of Theorem 19.45, p. 90, of Margaris. $)
    r19.45zv $p |- ( -. A = (/) ->
                   ( E. x e. A ( ph \/ ps ) <-> ( ph \/ E. x e. A ps ) ) ) $=
      ( c0 wceq wn wrex wo r19.9rzv birord r19.43 syl6rbbr ) DEFGZABCDHZIACDHZO
      IABICDHNAPOACDJKABCDLM $.
      $([27-May-98]$)
  $}

  ${
    $d x A $.
    $( Any wff is true when quantified over the empty set. $)
    rzal $p |- ( A = (/) -> A. x e. A ph ) $=
      ( c0 wceq cv wcel eleq2 noel pm2.21i syl6bi r19.21aiv ) CDEZABCMBFZCGNDGZ
      ACDNHOANIJKL $.
      $([11-Mar-97]$)

    $( Absorption law for restricted quantifier. $)
    ralabs $p |- ( A. x e. A ph <-> A. x e. A A. x e. A ph ) $=
      ( c0 wceq wral wb pm5.1 rzal sylan13 wn cv wcel wex nnull wi biimt wal
      df-ral hbra1 19.23 bitr syl6bbr sylbi pm2.61i ) CDEZABCFZUGBCFZGZUGUHUIUF
      UGUHHABCIUGBCIJUFKBLCMZBNZUIBCOUKUGUKUGPZUHUKUGQUHUJUGPBRULUGBCSUJUGBABCT
      UAUBUCUDUE $.
      $([28-Mar-97]$)
  $}

  ${
    $d y ph $.  $d x ps $.  $d x y A $.
    $( Rearrange restricted quantifiers. $)
    raaan   $p |- ( A. x e. A A. y e. A ( ph /\ ps ) <->
                  ( A. x e. A ph /\ A. y e. A ps ) ) $=
      ( c0 wceq wa wral wb pm5.1 rzal jca sylan13 wn r19.3rzv birand r19.26
      syl6rbbr biraldv biland bitrd pm2.61i ) EFGZABHDEIZCEIZACEIZBDEIZHZJZUFUI
      UJUDUFUIKUECELUDUGUHACELBDELMNUDOZUFAUHHZCEIZUIUKUEULCEUKULADEIZUHHUEUKAU
      NUHADEPQABDERSTUKUIUGUHCEIZHUMUKUHUOUGUHCEPUAAUHCERSUBUC $.
      $([11-Mar-97]$)
  $}



