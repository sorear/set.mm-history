$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The empty set; derive the Axiom of the Empty Set
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the empty or null set. $)
  $c (/) $. $( null set $)

  $( Extend class notation to include the empty set. $)
  c0  $a class (/) $.

  $( Define the empty set. $)
  df-nul $a |- (/) = ( V \ V ) $.

  $( Alternate definition of the empty set.  Definition 5.14 of Takeuti and
     Zaring, p. 20. $)
  dfnul2 $p |- (/) = { x | -. x = x } $=
      ( weq wn c0 cv wcel cvv cdif wa df-nul cleq4i eldif eqid pm3.24 2th
      bicon2 3bitr cleqabri ) AABZCZADAEZDFUAGGHZFUAGFZUCCIZTDUBUAJKUAGGLSUDSUD
      CAMUCNOPQR $.
      $([26-Dec-96]$)

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( An empty set has no elements.  Theorem 6.14 of Quine, p. 44. $)
    noel $p |- -. A e. (/) $=
      ( vx cvv wcel c0 wn cv wceq cleq3 weq dfnul2 cleqabi bicon2 eqid mpbi
      mtbii vtocle elisset con3 pm2.61i ) ACDZAEDZFZUCBABGZAHUDEDZUBUDAEIBBJZUE
      FUEUFUFFBEBKLMBNOPQUBUAAERST $.
  $}

  $( If a set has no elements, it is not empty. $)
  noel2 $p |- ( B e. A -> -. A = (/) ) $=
      ( c0 wceq wcel cleq4 noel mtbiri con2 ) ACDZBAEZJKBCEACBFBGHI $.
      $([31-Dec-93]$)

  ${
    $d x A $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       Takeuti and Zaring, p. 20. $)
    nnull $p |- ( -. A = (/) <-> E. x x e. A ) $=
      ( c0 wceq wn cv wcel wal wex wb dfcleq noel nbn bial bitr4 bineg df-ex )
      BCDZEAFZBGZEZAHZETAIRUBRTSCGZJZAHUBABCKUAUDAUCTSLMNOPTAQO $.
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Non-empty class abstraction. $)
    noel3 $p |- ( -. { x | ph } = (/) <-> E. x ph ) $=
      ( vy cv cab wcel wex c0 wceq wn hbab1 ax-17 cleq3 cbvex nnull abid biex
      3bitr4 ) CDZABEZFZCGBDZTFZBGTHIJABGUAUCCBABCKUCCLSUBTMNCTOAUCBABPQR $.
      $([26-Dec-96]$)
  $}

  ${
    $d x A $.
    $( An empty set has no elements.  Theorem 2, p. 22, of Suppes. $)
    nulleq $p |- ( A = (/) <-> A. x -. x e. A ) $=
      ( c0 wceq cv wcel wn wal wex nnull df-ex bitr bicon4 ) BCDZAEBFZGAHZNGOAI
      PGABJOAKLM $.
      $([29-Aug-93]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The intersection of a class with the empty set is the empty set.
       Theorem 16 of Suppes, p. 26. $)
    in0 $p |- (/) = ( A i^i (/) ) $=
      ( vx c0 cv wcel noel bianfal ineqri ) BCACBDZCEIAEIFGH $.
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The union of a class with the empty set is itself.  Theorem 24 of
       Suppes, p. 27. $)
    un0 $p |- A = ( A u. (/) ) $=
      ( vx c0 cv wcel noel biorfal uneqri ) BAACBDZCEIAEIFGH $.
  $}

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( The null set is a subset of any class.  Part of Exercise 1 of Takeuti
       and Zaring, p. 22. $)
    nullss $p |- (/) (_ A $=
      ( vx c0 cv wcel noel pm2.21i ssri ) BCABDZCEIAEIFGH $.
  $}

  $( Any subset of the empty set is empty.  Theorem 5 of Suppes, p. 23. $)
  ss0 $p |- ( A (_ (/) -> A = (/) ) $=
      ( c0 wss wceq wa eqss biimpr nullss mpan2 ) ABCZBACZABDZLJKEABFGAHI $.
      $([13-Aug-94]$)

  $( The null set is a proper subset of any non-empty class. $)
  nullpss $p |- ( (/) (. A <-> -. A = (/) ) $=
      ( c0 wpss wceq wn wss dfpss2 nullss mpbiran cleqcom bineg bitr ) BACZBADZ
      EZABDZEMBAFOBAGAHINPBAJKL $.
      $([27-Feb-96]$)

  ${
    $d A x $.  $d B x $.
    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj $p |- ( ( A i^i B ) = (/) <-> A. x ( x e. A -> -. x e. B ) ) $=
      ( cin c0 wceq cv wcel wa cab wb wal wn wi dfin2 cleq1i cleqrab imnan
      noel nbn bitr2 bial 3bitr ) BCDZEFAGZBHZUECHZIZAJZEFUHUEEHZKZALUFUGMNZALU
      DUIEABCOPUHAEQUKULAULUHMUKUFUGRUJUHUESTUAUBUC $.
      $([19-Aug-93]$)
  $}

  $( The intersection of classes with a common member is nonempty. $)
  inelcm $p |- ( ( A e. B /\ A e. C ) -> -. ( B i^i C ) = (/) ) $=
      ( wcel wa cin c0 wceq wn elin noel2 sylbir ) ABDACDEABCFZDMGHIABCJMAKL $.
      $([7-Apr-94]$)

  $( A minimum element of a class has no elements in common with the class. $)
  minel $p |- ( ( A e. B /\ ( C i^i B ) = (/) ) -> -. A e. C ) $=
      ( wcel cin c0 wceq wn wa wi inelcm con2 imnan sylibr pm2.03d comm imp )
      ABDZCBEFGZACDZHZSRUASTRSTRIZHTRHJUBSACBKLTRMNOPQ $.
      $([22-Jun-94]$)

  $( ` x ` is a dummy variable in the proof below. $)
  $( The Null Set axiom of ZF set theory:  the empty class exists.  Corollary
     5.16 of Takeuti and Zaring, p. 20. $)
  zfnull $p |- (/) e. V $=
      ( vx c0 cv cin cvv in0 visset zfauscl eqeltr ) BACZBDEJFJBAGHI $.

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass expressed in terms of difference.  Exercise 7 of Takeuti and
       Zaring, p. 22. $)
    ssdif0 $p |- ( A (_ B <-> ( A \ B ) = (/) ) $=
      ( vx cv wcel wi wal cdif wn wss c0 wceq wa iman eldif bineg bitr4 bial
      df-ss nulleq 3bitr4 ) CDZAEZUBBEZFZCGUBABHZEZIZCGABJUFKLUEUHCUEUCUDIMZIUH
      UCUDNUGUIUBABOPQRCABSCUFTUA $.
      $([29-Apr-94]$)
  $}

  $( A proper subclass has a nonempty difference. $)
  pssdifn0 $p |- ( ( A (_ B /\ -. A = B ) -> -. ( B \ A ) = (/) ) $=
      ( wss wceq wn cdif c0 wa eqss biimpr exp ssdif0 syl5ibr pm2.16d imp ) AB
      CZABDZEBAFGDZEPRQPBACZQRPSQQPSHABIJKBALMNO $.
      $([3-May-94]$)

  ${
    $d x A $.  $d x B $.
    $( A proper subclass has a member in one argument that's not in both. $)
    pssnel $p |- ( A (. B -> E. x ( x e. B /\ -. x e. A ) ) $=
      ( wpss cv cdif wcel wex wn wa c0 wceq wss dfpss2 pssdifn0 sylbi nnull
      sylib eldif biex ) BCDZAEZCBFZGZAHZUBCGUBBGIJZAHUAUCKLIZUEUABCMBCLIJUGBCN
      BCOPAUCQRUDUFAUBCBSTR $.
      $([29-Feb-96]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Difference, intersection, and subclass relationship. $)
    difin0ss $p |- ( ( ( A \ B ) i^i C ) = (/) -> ( C (_ A -> C (_ B ) ) $=
      ( vx cdif cin c0 wceq cv wcel wi wal wss wn nulleq wa annim bilan ancom
      bitr3 bineg iman elin eldif biran bitr 3bitr4 ax-2 sylbir 19.20ii sylbi
      df-ss 3imtr4g ) ABEZCFZGHZDIZCJZUQAJZKZDLZURUQBJZKZDLZCAMCBMUPUQUOJZNZDLV
      AVDKDUOOVFUTVCDVFURUSVBKZKZUTVCKURVGNZPZNUSVBNPZURPZNVHVFVJVLVJURVKPVLVKV
      IURUSVBQRURVKSTUAURVGUBVEVLVEUQUNJZURPVLUQUNCUCVMVKURUQABUDUEUFUAUGURUSVB
      UHUIUJUKDCAULDCBULUM $.
      $([30-Apr-94]$)

    $( Intersection, subclass, and difference relationship. $)
    inssdif0 $p |- ( ( A i^i B ) (_ C <-> ( A i^i ( B \ C ) ) = (/) ) $=
      ( vx cv cin wcel wi wal cdif wn wss c0 wceq wa impexp iman bilim imnan
      3bitr elin birim eldif bilan bitr bineg 3bitr4 bial df-ss nulleq ) DEZABF
      ZGZUKCGZHZDIUKABCJZFZGZKZDIULCLUQMNUOUSDUKAGZUKBGZOZUNHZUTVAUNKOZOZKZUOUS
      VCUTVAUNHZHUTVDKZHVFUTVAUNPVGVHUTVAUNQRUTVDSTUMVBUNUKABUAUBURVEURUTUKUPGZ
      OVEUKAUPUAVIVDUTUKBCUCUDUEUFUGUHDULCUIDUQUJUG $.
      $([27-Oct-96]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The difference between a class and itself is the empty set.
       Generalization of Proposition 5.15, p. 20, of Takeuti and Zaring. $)
    difid $p |- (/) = ( A \ A ) $=
      ( vx weq wn cab cv wcel wa c0 cdif pm3.24 eqid 2th bicon1 biab dfnul2
      df-dif 3eqtr4 ) BBCZDZBEBFAGZUADHZBEIAAJTUBBUBSUBDSUAKBLMNOBPBAAQR $.
      $([16-Sep-93]$)
  $}

  ${
    $d x A $.  $d x ph $.
    $( Restricted quantification of wff not containing quantified variable. $)
    r19.3rzv $p |- ( -. A = (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wceq wn cv wcel wex wi wral wb nnull biimtrut sylbi wal df-ral
      ax-17 19.23 bitr syl6bbr ) CDEFZABGCHZBIZAJZABCKZUBUDAUELBCMUDANOUFUCAJBP
      UEABCQUCABABRSTUA $.
      $([10-Mar-97]$)
  $}

  ${
    $d x A $.
    $( Any wff is true when quantified over the empty set. $)
    rzal $p |- ( A = (/) -> A. x e. A ph ) $=
      ( c0 wceq cv wcel cleq4 wn wi pm2.21 noel ax-mp syl6bi comm r19.21aiv )
      CDEZABCQBFZCGZAQSRDGZACDRHTITAJTAKRLMNOP $.
      $([11-Mar-97]$)

    $( Absorption law for restricted quantifier. $)
    ralabs $p |- ( A. x e. A ph <-> A. x e. A A. x e. A ph ) $=
      ( c0 wceq wral wb wa rzal jca pm5.1 syl wn cv wcel wex nnull wi biimtrut
      wal df-ral hbra1 19.23 bitr syl6bbr sylbi pm2.61i ) CDEZABCFZUIBCFZGZUHUI
      UJHUKUHUIUJABCIUIBCIJUIUJKLUHMBNCOZBPZUKBCQUMUIUMUIRZUJUMUISUJULUIRBTUNUI
      BCUAULUIBABCUBUCUDUEUFUG $.
      $([28-Mar-97]$)
  $}

  ${
    $d y ph $.  $d x ps $.  $d x y A $.
    $( Rearrange restricted quantifiers. $)
    raaan   $p |- ( A. x e. A A. y e. A ( ph /\ ps ) <->
                  ( A. x e. A ph /\ A. y e. A ps ) ) $=
      ( c0 wceq wa wral wb rzal jca pm5.1 syl wn r19.3rzv birand r19.26
      syl6rbbr biraldv biland bitrd pm2.61i ) EFGZABHDEIZCEIZACEIZBDEIZHZJZUDUF
      UIHUJUDUFUIUECEKUDUGUHACEKBDEKLLUFUIMNUDOZUFAUHHZCEIZUIUKUEULCEUKULADEIZU
      HHUEUKAUNUHADEPQABDERSTUKUIUGUHCEIZHUMUKUHUOUGUHCEPUAAUHCERSUBUC $.
      $([11-Mar-97]$)
  $}



