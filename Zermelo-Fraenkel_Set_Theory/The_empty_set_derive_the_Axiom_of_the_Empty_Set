$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The empty set; derive the Axiom of the Empty Set
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the empty or null set. $)
  $c (/) $. $( null set $)

  $( Extend class notation to include the empty set. $)
  c0 $a class (/) $.

  $( Define the empty set.  Special case of Exercise 4.10(o) of [Mendelson]
     p. 231.  For a more traditional definition, but requiring a dummy
     variable, see ~ dfnul2 . $)
  df-nul $a |- (/) = ( V \ V ) $.

  $( Alternate definition of the empty set.  Definition 5.14 of [TakeutiZaring]
     p. 20. $)
  dfnul2 $p |- (/) = { x | -. x = x } $=
    ( weq wn c0 cv wcel cvv cdif wa df-nul eleq2i eldif cleqid pm3.24 2th
    bicon2 3bitr biabri ) AABZCZADAEZDFUAGGHZFUAGFZUCCIZTDUBUAJKUAGGLSUDSUDCUAM
    UCNOPQR $.
    $( [26-Dec-96] $)

  $( Alternate definition of the empty set.. $)
  dfnul3 $p |- (/) = { x e. A | -. x e. A } $=
    ( weq wn cab cv wcel wa c0 crab pm3.24 cleqid 2th bicon1 biabi dfnul2
    df-rab 3eqtr4 ) AACZDZAEAFZBGZUBDZHZAEIUCABJTUDAUDSUDDSUBKUALMNOAPUCABQR $.
    $( [26-Mar-04] $) $( [25-Mar-04] $)

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( The empty set has no elements.  Theorem 6.14 of [Quine] p. 44. $)
    noel $p |- -. A e. (/) $=
      ( vx cvv wcel c0 wn cv wceq weq cleqid dfnul2 cleqabi bicon2 mpbi eleq1
      mtbii vtocleg elisset con3i pm2.61i ) ACDZAEDZFZUCBACBGZAHUDEDZUBBBIZUEFU
      DJUEUFUFFBEBKLMNUDAEOPQUBUAAERST $.
      $( [5-Aug-93] $)
  $}

  $( If a set has elements, it is not empty. $)
  noel2 $p |- ( B e. A -> -. A = (/) ) $=
    ( c0 wceq wcel noel eleq2 mtbiri con2i ) ACDZBAEZJKBCEBFACBGHI $.
    $( [31-Dec-93] $)

  ${
    $d x y $.  $d y A $.
    nnullf.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       [TakeutiZaring] p. 20.  This version of ~ n0 requires only that
       ` x ` not be free in, rather than not occur in, ` A ` . $)
    n0f $p |- ( -. A = (/) <-> E. x x e. A ) $=
      ( c0 wceq wn cv wcel wal wex wb ax-17 cleqf noel nbn bial bitr4 bineg
      df-ex ) CEFZGAHZCIZGZAJZGUCAKUAUEUAUCUBEIZLZAJUEABCEDBHEIAMNUDUGAUFUCUBOP
      QRSUCATR $.
      $( [23-Oct-03] $) $( [17-Oct-03] $)
  $}

  ${
    $d x y A $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       [TakeutiZaring] p. 20. $)
    n0 $p |- ( -. A = (/) <-> E. x x e. A ) $=
      ( vy cv wcel ax-17 n0f ) ACBCDBEAFG $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x y $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Non-empty class abstraction. $)
    noel3 $p |- ( -. { x | ph } = (/) <-> E. x ph ) $=
      ( vy cab c0 wceq wn cv wcel wex n0 hbab1 ax-17 eleq1 cbvex abid biex
      3bitr ) ABDZEFGCHZSIZCJBHZSIZBJABJCSKUAUCCBABCLUCCMTUBSNOUCABABPQR $.
      $( [26-Dec-96] $)
  $}

  $( Non-empty restricted class abstraction. $)
  rnoel3 $p |- ( -. { x e. A | ph } = (/) <-> E. x e. A ph ) $=
    ( cv wcel wa cab c0 wceq wn wex crab wrex noel3 df-rab cleq1i bineg df-rex
    3bitr4 ) BDCEAFZBGZHIZJTBKABCLZHIZJABCMTBNUDUBUCUAHABCOPQABCRS $.
    $( [29-Aug-99] $)

  $( Existential quantification restricted to the empty set is false. $)
  rex0 $p |- -. E. x e. (/) ph $=
    ( c0 cv wcel wn noel pm2.21i nrex ) ABCBDZCEAFJGHI $.
    $( [16-Oct-03] $) $( [15-Oct-03] $)

  $( Any restricted abstraction class restricted to the empty set is empty. $)
  rab0 $p |- { x e. (/) | ph } = (/) $=
    ( cv c0 wcel wa wex wn crab wceq noel intnanr nex wrex rnoel3 df-rex bitr
    bicon1 mpbi ) BCZDEZAFZBGZHABDIDJZUBBUAATKLMUDUCUDHABDNUCABDOABDPQRS $.
    $( [16-Oct-03] $) $( [15-Oct-03] $)

  ${
    $d x A $.
    $( The empty set has no elements.  Theorem 2 of [Suppes] p. 22. $)
    eq0 $p |- ( A = (/) <-> A. x -. x e. A ) $=
      ( c0 wceq cv wcel wn wal wex n0 df-ex bitr bicon4 ) BCDZAEBFZGAHZNGOAIPGA
      BJOAKLM $.
      $( [29-Aug-93] $)
  $}

  ${
    $d x y A $.
    $( Membership of the empty set in another class. $)
    0el $p |- ( (/) e. A <-> E. x e. A A. y -. y e. x ) $=
      ( c0 wcel cv wceq wrex wel wn wal risset eq0 birex bitr ) DCEAFZDGZACHBAI
      JBKZACHADCLQRACBPMNO $.
      $( [30-Jun-04] $) $( [29-Jun-04] $)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( The union of a class with the empty set is itself.  Theorem 24 of
       [Suppes] p. 27. $)
    un0 $p |- ( A u. (/) ) = A $=
      ( vx c0 cv wcel wo noel biorfi bicom uneqri ) BACABDZAEZLKCEZFMLKGHIJ $.
      $( [5-Aug-93] $)

    $( The intersection of a class with the empty set is the empty set.
       Theorem 16 of [Suppes] p. 26. $)
    in0 $p |- ( A i^i (/) ) = (/) $=
      ( vx c0 cv wcel wa noel bianfi bicom ineqri ) BACCBDZCEZKAEZLFLMKGHIJ $.
      $( [5-Aug-93] $)
  $}

  $( The intersection of a class with the universal class is itself.  Exercise
     4.10(k) of [Mendelson] p. 231. $)
  inv $p |- ( A i^i V ) = A $=
    ( cvv cin inss1 ssid ssv ssini eqssi ) ABCAABDAABAEAFGH $.
    $( [17-May-98] $)

  $( The union of a class with the universal class is the universal class.
     Exercise 4.10(l) of [Mendelson] p. 231. $)
  unv $p |- ( A u. V ) = V $=
    ( cvv cun ssv ssun2 eqssi ) ABCZBGDBAEF $.
    $( [17-May-98] $)

  ${
    $d A x $.  $( ` x ` is dummy. $)
    $( The null set is a subset of any class.  Part of Exercise 1 of
       [TakeutiZaring] p. 22. $)
    0ss $p |- (/) (_ A $=
      ( vx c0 cv wcel noel pm2.21i ssriv ) BCABDZCEIAEIFGH $.
      $( [5-Aug-93] $)
  $}

  $( Any subset of the empty set is empty.  Theorem 5 of [Suppes] p. 23 and its
     converse. $)
  ss0b $p |- ( A (_ (/) <-> A = (/) ) $=
    ( c0 wss wceq 0ss wa eqss biimpr mpan2 eqimss impbi ) ABCZABDZLBACZMAEMLNFA
    BGHIABJK $.
    $( [17-Sep-03] $)

  $( Any subset of the empty set is empty.  Theorem 5 of [Suppes] p. 23. $)
  ss0 $p |- ( A (_ (/) -> A = (/) ) $=
    ( c0 wss wceq ss0b biimp ) ABCABDAEF $.
    $( [13-Aug-94] $)

  $( Two classes are empty iff their union is empty. $)
  un00 $p |- ( ( A = (/) /\ B = (/) ) <-> ( A u. B ) = (/) ) $=
    ( c0 wceq wa cun uneq12 un0 syl6eq wss ssun1 sseq2 mpbii ss0b sylib ssun2
    jca impbi ) ACDZBCDZEZABFZCDZUAUBCCFCACBCGCHIUCSTUCACJZSUCAUBJUDABKUBCALMAN
    OUCBCJZTUCBUBJUEBAPUBCBLMBNOQR $.
    $( [12-Aug-04] $) $( [11-Aug-04] $)

  $( Only the universal class has the universal class as a subclass. $)
  vss $p |- ( V (_ A <-> A = V ) $=
    ( cvv wss wceq wa ssv jctl eqss sylibr eqimss2 impbi ) BACZABDZLABCZLEMLNAF
    GABHIBAJK $.
    $( [17-Sep-03] $)

  $( The null set is a proper subset of any non-empty set. $)
  0pss $p |- ( (/) (. A <-> -. A = (/) ) $=
    ( c0 wpss wceq wn wss dfpss2 0ss mpbiran cleqcom bineg bitr ) BACZBADZEZABD
    ZEMBAFOBAGAHINPBAJKL $.
    $( [27-Feb-96] $)

  $( No set is a proper subset of the empty set. $)
  npss0 $p |- -. A (. (/) $=
    ( c0 wpss wceq cleqid wn wss wb pssss ss0 psseq1 3syl ibi 0pss sylib mt2 )
    ABCZBBDZBEQBBCZRFQSQABGABDQSHABIAJABBKLMBNOP $.
    $( [17-Jun-98] $)

  $( Any non-universal class is a proper subclass of the universal class. $)
  pssv $p |- ( A (. V <-> -. A = V ) $=
    ( cvv wpss wss wceq wn dfpss2 ssv mpbiran ) ABCABDABEFABGAHI $.
    $( [17-May-98] $)

  ${
    $d A x $.  $d B x $.
    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj $p |- ( ( A i^i B ) = (/) <-> A. x e. A -. x e. B ) $=
      ( cin c0 wceq cv wcel wn wi wal wral wa cab wb df-in cleq1i cleqabr
      imnan noel nbn bitr2 bial 3bitr df-ral bitr4 ) BCDZEFZAGZBHZUICHZIZJZAKZU
      LABLUHUJUKMZANZEFUOUIEHZOZAKUNUGUPEABCPQUOAERURUMAUMUOIURUJUKSUQUOUITUAUB
      UCUDULABUEUF $.
      $( [17-Feb-04] $) $( [17-Feb-04] $)

    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj1 $p |- ( ( A i^i B ) = (/) <-> A. x ( x e. A -> -. x e. B ) ) $=
      ( cin c0 wceq cv wcel wn wral wi wal disj df-ral bicom bitr4 ) BCDEFAGZCH
      IZABJZQBHRKALZABCMSTRABNOP $.
      $( [19-Aug-93] $)

    $( Two ways of saying that two classes are disjoint. $)
    disj2 $p |- ( ( A i^i B ) = (/) <-> A (_ ( V \ B ) ) $=
      ( vx cv wcel wn wi wal cvv cdif cin c0 wceq wss wa visset biantrur eldif
      bitr4 bilim bial disj1 dfss2 3bitr4 ) CDZAEZUEBEFZGZCHUFUEIBJZEZGZCHABKLM
      AUINUHUKCUGUJUFUGUEIEZUGOUJULUGCPQUEIBRSTUACABUBCAUIUCUD $.
      $( [17-May-98] $)

    $( Two ways of saying that two classes are disjoint. $)
    disj3 $p |- ( ( A i^i B ) = (/) <-> A = ( A \ B ) ) $=
      ( vx cv wcel wn wi wal cdif wb cin c0 wceq wa pm4.71 eldif bilbi bitr4
      bial disj1 dfcleq 3bitr4 ) CDZAEZUCBEFZGZCHUDUCABIZEZJZCHABKLMAUGMUFUICUF
      UDUDUENZJUIUDUEOUHUJUDUCABPQRSCABTCAUGUAUB $.
      $( [19-May-98] $)
  $}

  $( Two ways of saying that two classes are disjoint. $)
  disj4 $p |- ( ( A i^i B ) = (/) <-> -. ( A \ B ) (. A ) $=
    ( cin c0 wceq cdif wpss wn disj3 cleqcom wss dfpss2 difss mpbiran bicon2
    3bitr ) ABCDEAABFZEQAEZQAGZHABIAQJSRSQAKRHQALABMNOP $.
    $( [22-Mar-04] $) $( [21-Mar-04] $)

  $( A class is a proper subset of its union with a disjoint nonempty class. $)
  disjpss $p |- ( ( ( A i^i B ) = (/) /\ -. B = (/) ) -> A (. ( A u. B ) ) $=
    ( cin c0 wceq wn wa wss cun wpss sseq2 ssid biantru ssin bitr syl5bb ss0
    syl6bi con3d imp nsspssun uncom psseq2i sylib ) ABCZDEZBDEZFZGBAHZFZAABIZJZ
    UFUHUJUFUIUGUFUIBDHZUGUFBUEHZUMUIUEDBKUIUIBBHZGUNUOUIBLMBABNOPBQRSTUJABAIZJ
    ULBAUAUPUKABAUBUCOUD $.
    $( [?] $) $( [15-Sep-04] $)

  $( The union of disjoint classes is disjoint. $)
  undisj $p |- ( ( ( A i^i B ) = (/) /\ ( A i^i C ) = (/) ) <->
               ( A i^i ( B u. C ) ) = (/) ) $=
    ( cin c0 wceq wa cun un00 indi cleq1i bitr4 ) ABDZEFACDZEFGMNHZEFABCHDZEFMN
    IPOEABCJKL $.
    $( [?] $) $( [13-Sep-04] $)

  $( Subclass expressed in terms of intersection with difference from the
     universal class. $)
  ssindif0 $p |- ( A (_ B <-> ( A i^i ( V \ B ) ) = (/) ) $=
    ( cvv cdif cin c0 wceq wss disj2 ddif sseq2i bitr2 ) ACBDZEFGACMDZHABHAMINB
    ABJKL $.
    $( [17-Sep-03] $)

  $( The intersection of classes with a common member is nonempty. $)
  inelcm $p |- ( ( A e. B /\ A e. C ) -> -. ( B i^i C ) = (/) ) $=
    ( wcel wa cin c0 wceq wn elin noel2 sylbir ) ABDACDEABCFZDMGHIABCJMAKL $.
    $( [7-Apr-94] $)

  $( A minimum element of a class has no elements in common with the class. $)
  minel $p |- ( ( A e. B /\ ( C i^i B ) = (/) ) -> -. A e. C ) $=
    ( wcel cin c0 wceq wn wa wi inelcm con2i imnan sylibr con2d com12 imp ) ABD
    ZCBEFGZACDZHZSRUASTRSTRIZHTRHJUBSACBKLTRMNOPQ $.
    $( [22-Jun-94] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Distribute union over difference. $)
    undif4 $p |- ( ( A i^i C ) = (/) ->
                 ( A u. ( B \ C ) ) = ( ( A u. B ) \ C ) ) $=
      ( vx cv wcel wn wi wal cdif cun wb cin c0 wceq wo wa pm2.61 ax-1 a1i
      impbid df-or syl5bb biland eldif bilor ordi bitr elun biran 3bitr4g
      19.20i disj1 dfcleq 3imtr4 ) DEZAFZUPCFGZHZDIUPABCJZKZFZUPABKZCJZFZLZDIAC
      MNOVAVDOUSVFDUSUQUPUTFZPZUPVCFZURQZVBVEUSUQUPBFZPZUQURPZQZVLURQVHVJUSVMUR
      VLUSUQGZURHZURVMUSVPURUQURRURVPHUSURVOSTUAUQURUBUCUDVHUQVKURQZPVNVGVQUQUP
      BCUEUFUQVKURUGUHVIVLURUPABUIUJUKUPAUTUIUPVCCUEUKULDACUMDVAVDUNUO $.
      $( [17-May-98] $)
  $}

  $( ` x ` is a dummy variable in the proof below. $)
  $( The Null Set axiom of ZF set theory:  the empty set exists.  Corollary
     5.16 of [TakeutiZaring] p. 20.  In some textbooks this is presented as
     a separate axiom; here we show it can be derived from the Extensionality
     and  Replacement axioms.  It cannot be derived from these unless our
     predicate calculus includes an axiom stating that at least one set exists
     (which it does in the form of ~ ax-9 ).  For another version, see
     ~ zfnul . $)
  0ex $p |- (/) e. V $=
    ( vx cv c0 cin cvv in0 visset inex1 eqeltrr ) ABZCDCEJFJCAGHI $.
    $( [5-Aug-93] $)

  ${
    $d x y z $.
    $( Another version of the Null Set axiom, expressed in terms of logical
       primitives and proved directly from Aussonderung.  Axiom of Empty Set
       of [Enderton] p. 18. $)
    zfnul $p |- E. x A. y -. y e. x $=
      ( vz wel wn wa wb wal wex cv visset zfaus pm3.24 id mtbiri 19.20i 19.22i
      ax-mp ) BADZBCDZTEZFZGZBHZAISEZBHZAIUABACJCKLUDUFAUCUEBUCSUBTMUCNOPQR $.
      $( [7-Aug-03] $)
  $}

  ${
    $d x A $.
    $( Construct, from any class ` A ` , a set equal to it when the class
       exists and equal to the empty set when the class is proper.  This
       theorem shows that the constructed set always exists. $)
    class2set $p |- { x e. A | A e. V } e. V $=
      ( cvv wcel crab rabexg wn c0 0ex wrex wceq cv pm3.26 nrexdv rnoel3
      bicon1 sylib eleq1d mpbiri pm2.61i ) BCDZUAABEZCDZUAABCFUAGZUCHCDIUDUBHCU
      DUAABJZGUBHKZUDUAABUDALBDMNUFUEUAABOPQRST $.
      $( [15-Nov-03] $) $( [16-Oct-03] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subclass expressed in terms of difference.  Exercise 7 of [TakeutiZaring]
       p. 22. $)
    ssdif0 $p |- ( A (_ B <-> ( A \ B ) = (/) ) $=
      ( vx cv wcel wi wal cdif wn wss c0 wceq wa iman eldif bineg bitr4 bial
      dfss2 eq0 3bitr4 ) CDZAEZUBBEZFZCGUBABHZEZIZCGABJUFKLUEUHCUEUCUDIMZIUHUCU
      DNUGUIUBABOPQRCABSCUFTUA $.
      $( [29-Apr-94] $)
  $}

  $( Universal class equality in terms of empty difference. $)
  vdif0 $p |- ( A = V <-> ( V \ A ) = (/) ) $=
    ( cvv wceq wss cdif c0 vss ssdif0 bitr3 ) ABCBADBAEFCAGBAHI $.
    $( [17-Sep-03] $)

  $( A proper subclass has a nonempty difference. $)
  pssdifn0 $p |- ( ( A (_ B /\ -. A = B ) -> -. ( B \ A ) = (/) ) $=
    ( wss wceq wn cdif c0 wa eqss biimpr exp ssdif0 syl5ibr con3d imp ) ABCZABD
    ZEBAFGDZEPRQPBACZQRPSQQPSHABIJKBALMNO $.
    $( [3-May-94] $)

  $( A subclass missing a member is a proper subclass. $)
  ssnelpss $p |- ( A (_ B -> ( ( C e. B /\ -. C e. A ) -> A (. B ) ) $=
    ( wss wceq wn wpss wcel wa dfpss2 baibr clneq2 cleqcom bineg sylib syl5bi
    ) ABDZABEZFZABGZCBHCAHFIZTQSABJKUABAEZFSCBALUBRBAMNOP $.
    $( [12-Jan-02] $)

  ${
    $d x A $.  $d x B $.
    $( A proper subclass has a member in one argument that's not in both. $)
    pssnel $p |- ( A (. B -> E. x ( x e. B /\ -. x e. A ) ) $=
      ( wpss cv cdif wcel wex wn wa c0 wceq wss dfpss2 pssdifn0 sylbi n0 sylib
      eldif biex ) BCDZAEZCBFZGZAHZUBCGUBBGIJZAHUAUCKLIZUEUABCMBCLIJUGBCNBCOPAU
      CQRUDUFAUBCBSTR $.
      $( [29-Feb-96] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Difference, intersection, and subclass relationship. $)
    difin0ss $p |- ( ( ( A \ B ) i^i C ) = (/) -> ( C (_ A -> C (_ B ) ) $=
      ( vx cdif cin c0 wceq cv wcel wi wal wss wn eq0 wa annim bilan ancom
      bitr3 bineg iman elin eldif biran bitr 3bitr4 ax-2 sylbir 19.20ii sylbi
      dfss2 3imtr4g ) ABEZCFZGHZDIZCJZUQAJZKZDLZURUQBJZKZDLZCAMCBMUPUQUOJZNZDLV
      AVDKDUOOVFUTVCDVFURUSVBKZKZUTVCKURVGNZPZNUSVBNPZURPZNVHVFVJVLVJURVKPVLVKV
      IURUSVBQRURVKSTUAURVGUBVEVLVEUQUNJZURPVLUQUNCUCVMVKURUQABUDUEUFUAUGURUSVB
      UHUIUJUKDCAULDCBULUM $.
      $( [30-Apr-94] $)

    $( Intersection, subclass, and difference relationship. $)
    inssdif0 $p |- ( ( A i^i B ) (_ C <-> ( A i^i ( B \ C ) ) = (/) ) $=
      ( vx cv cin wcel wi wal cdif wn wss c0 wceq wa impexp iman bilim imnan
      3bitr elin birim eldif bilan bitr bineg 3bitr4 bial dfss2 eq0 ) DEZABFZGZ
      UKCGZHZDIUKABCJZFZGZKZDIULCLUQMNUOUSDUKAGZUKBGZOZUNHZUTVAUNKOZOZKZUOUSVCU
      TVAUNHZHUTVDKZHVFUTVAUNPVGVHUTVAUNQRUTVDSTUMVBUNUKABUAUBURVEURUTUKUPGZOVE
      UKAUPUAVIVDUTUKBCUCUDUEUFUGUHDULCUIDUQUJUG $.
      $( [27-Oct-96] $)
  $}

  $( The difference between a class and itself is the empty set.   Proposition
     5.15 of [TakeutiZaring] p. 20.  Also Theorem 32 of [Suppes] p. 28. $)
  difid $p |- ( A \ A ) = (/) $=
    ( wss cdif c0 wceq ssid ssdif0 mpbi ) AABAACDEAFAAGH $.
    $( [13-May-04] $) $( [22-Apr-04] $)

  $( The difference between a class and the empty set.  Part of Exercise 4.4 of
     [Stoll] p. 16. $)
  dif0 $p |- ( A \ (/) ) = A $=
    ( cdif c0 difid difeq2i difdif eqtr3 ) AAABZBACBAHCAADEAAFG $.
    $( [18-Aug-04] $) $( [17-Aug-04] $)

  $( The difference between the empty set and a class.  Part of Exercise 4.4 of
     [Stoll] p. 16. $)
  0dif $p |- ( (/) \ A ) = (/) $=
    ( c0 cdif wss wceq difss ss0 ax-mp ) BACZBDIBEBAFIGH $.
    $( [8-Sep-04] $) $( [17-Aug-04] $)

  $( The intersection of a class and its relative complement is empty.  Theorem
     38 of [Suppes] p. 29. $)
  indif0 $p |- ( A i^i ( B \ A ) ) = (/) $=
    ( cin wss cdif c0 wceq inss1 inssdif0 mpbi ) ABCADABAECFGABHABAIJ $.
    $( [24-Mar-98] $)

  $( The difference of a class from its intersection is empty.  Theorem 37 of
     [Suppes] p. 29. $)
  difin0 $p |- ( ( A i^i B ) \ B ) = (/) $=
    ( cin cdif c0 difindir difid ineq2i in0 3eqtr ) ABCBDABDZBBDZCKECEABBFLEKBG
    HKIJ $.
    $( [?] $) $( [17-Aug-04] $)

  $( The union of a class and its complement is the universe.  Theorem 5.1(5)
     of [Stoll] p. 17. $)
  undifv $p |- ( A u. ( V \ A ) ) = V $=
    ( cvv cdif cun cin c0 dfun3 indif0 difeq2i dif0 3eqtr ) ABACZDBLBLCEZCBFCBA
    LGMFBLBHIBJK $.
    $( [18-Aug-04] $) $( [17-Aug-04] $)

  $( Absorption of difference by union.  This decomposes a union into two
     disjoint classes (see ~ indif0 ).  Theorem 35 of [Suppes] p. 29. $)
  undif1 $p |- ( ( A \ B ) u. B ) = ( A u. B ) $=
    ( cvv cdif cin cun invdif uneq1i undir uncom undifv eqtr ineq2i inv 3eqtr
    eqtr3 ) ACBDZEZBFZABDZBFABFZRTBABGHSUAQBFZEUACEUAAQBIUBCUAUBBQFCQBJBKLMUANO
    P $.
    $( [18-Aug-04] $) $( [19-May-98] $)

  $( Absorption of difference by union.  This decomposes a union into two
     disjoint classes (see ~ indif0 ).  Part of proof of Corollary 6K of
     [Enderton] p. 144. $)
  undif2 $p |- ( A u. ( B \ A ) ) = ( A u. B ) $=
    ( cdif cun uncom undif1 3eqtr ) ABACZDHADBADABDAHEBAFBAEG $.
    $( [4-Sep-04] $) $( [19-May-98] $)

  $( Absorption of union by difference.  Theorem 36 of [Suppes] p. 29. $)
  difun2 $p |- ( ( A u. B ) \ B ) = ( A \ B ) $=
    ( cun cdif c0 difundir difid uneq2i un0 3eqtr ) ABCBDABDZBBDZCKECKABBFLEKBG
    HKIJ $.
    $( [18-Aug-04] $) $( [19-May-98] $)

  $( Union of complementary parts into whole. $)
  ssundif $p |- ( A (_ B <-> B = ( A u. ( B \ A ) ) ) $=
    ( wss cun wceq cdif ssequn2 uncom undif1 eqtr cleq2i bitr4 ) ABCBBADZEBABAF
    ZDZEABGOMBONADMANHBAIJKL $.
    $( [22-Mar-98] $)

  $( Distributive law for class difference.  Exercise 4.8 of [Stoll] p. 16. $)
  difdifdir $p |- ( ( A \ B ) \ C ) = ( ( A \ C ) \ ( B \ C ) ) $=
    ( cdif cvv cun cin c0 indif0 incom eqtr3 uneq2i invdif un0 dif23 3eqtr4r
    indi 3eqtr4 indm difeq2i ddif 3eqtr3r ineq2i 3eqtr ) ABDCDZACDZEBDZCFZGZUFE
    BCDZDZGUFUJDUFUGGZHFZULUFCGZFUEUIHUNULCUFGHUNCAICUFJKLULUFBDUMUEUFBMULNABCO
    PUFUGCQRUHUKUFEBECDZGZDUGEUODZFUKUHBUOSUPUJEBCMTUQCUGCUALUBUCUFUJMUD $.
    $( [?] $) $( [18-Aug-04] $)

  ${
    $d x A $.
    $( Theorem 19.2 of [Margaris] p. 89 with restricted quantifiers (compare
       ~ 19.2 ).  The restricted version is valid only when the domain of
       quantification is not empty. $)
    r19.2z $p |- ( -. A = (/) -> ( A. x e. A ph -> E. x e. A ph ) ) $=
      ( wral c0 wceq wn wrex cv wcel wex wa wi wal df-ral exintr sylbi n0
      df-rex 3imtr4g com12 ) ABCDZCEFGZABCHZUBBICJZBKZUEALBKZUCUDUBUEAMBNUFUGMA
      BCOUEABPQBCRABCSTUA $.
      $( [17-Nov-03] $) $( [15-Nov-03] $)
  $}

  ${
    $d x A $.  $d x ph $.
    $( Restricted quantification of wff not containing quantified variable. $)
    r19.3rzv $p |- ( -. A = (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wceq wn cv wcel wex wi wral wb n0 biimt sylbi wal df-ral 19.23v
      bitr syl6bbr ) CDEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABRS
      T $.
      $( [10-Mar-97] $)

    $( Restricted quantification of wff not containing quantified variable. $)
    r19.9rzv $p |- ( -. A = (/) -> ( ph <-> E. x e. A ph ) ) $=
      ( c0 wceq wn wral wrex r19.3rzv bicomd bicon2d dfrex2 syl6bbr ) CDEFZAAFZ
      BCGZFABCHNPANOPOBCIJKABCLM $.
      $( [27-May-98] $)

    $( Restricted quantifier version of Theorem 19.28 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.28zv $p |- ( -. A = (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( ph /\ A. x e. A ps ) ) ) $=
      ( c0 wceq wn wral wa r19.3rzv birand r19.26 syl6rbbr ) DEFGZABCDHZIACDHZO
      IABICDHNAPOACDJKABCDLM $.
      $( [2-Sep-04] $) $( [19-Aug-04] $)

    $( Restricted version of Theorem 19.45 of [Margaris] p. 90. $)
    r19.45zv $p |- ( -. A = (/) ->
                   ( E. x e. A ( ph \/ ps ) <-> ( ph \/ E. x e. A ps ) ) ) $=
      ( c0 wceq wn wrex wo r19.9rzv birord r19.43 syl6rbbr ) DEFGZABCDHZIACDHZO
      IABICDHNAPOACDJKABCDLM $.
      $( [27-May-98] $)
  $}

  ${
    $d x A $.  $d x ps $.
    $( Restricted quantifier version of Theorem 19.27 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.27zv $p |- ( -. A = (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( A. x e. A ph /\ ps ) ) ) $=
      ( c0 wceq wn wral wa r19.3rzv biland r19.26 syl6rbbr ) DEFGZACDHZBIOBCDHZ
      IABICDHNBPOBCDJKABCDLM $.
      $( [27-Aug-04] $) $( [19-Aug-04] $)

    $( Restricted quantifier version of Theorem 19.36 of [Margaris] p. 90.
       It is valid only when the domain of quantification is not empty. $)
    r19.36zv $p |- ( -. A = (/) ->
                   ( E. x e. A ( ph -> ps ) <-> ( A. x e. A ph -> ps ) ) ) $=
      ( c0 wceq wn wral wrex wi r19.9rzv bilimd bicomd r19.35 syl5bb ) DEFGZACD
      HZBCDIZJZQBJZABJCDIPTSPBRQBCDKLMABCDNO $.
      $( [20-Sep-03] $)
  $}

  ${
    $d x A $.
    $( Vacuous quantification makes any wff true. $)
    rzal $p |- ( A = (/) -> A. x e. A ph ) $=
      ( c0 wceq cv wcel eleq2 noel pm2.21i syl6bi r19.21aiv ) CDEZABCMBFZCGNDGZ
      ACDNHOANIJKL $.
      $( [11-Mar-97] $)

    $( Idempotent law for restricted quantifier. $)
    ralidm $p |- ( A. x e. A A. x e. A ph <-> A. x e. A ph ) $=
      ( c0 wceq wral wb pm5.1 rzal sylan13 wn cv wcel wex n0 wi biimt wal
      df-ral hbra1 19.23 bitr syl6rbbr sylbi pm2.61i ) CDEZABCFZBCFZUGGZUHUGUIU
      FUHUGHUGBCIABCIJUFKBLCMZBNZUIBCOUKUGUKUGPZUHUKUGQUHUJUGPBRULUGBCSUJUGBABC
      TUAUBUCUDUE $.
      $( [28-Mar-97] $)
  $}

  ${
    $d y ph $.  $d x ps $.  $d x y A $.
    $( Rearrange restricted quantifiers. $)
    raaan $p |- ( A. x e. A A. y e. A ( ph /\ ps ) <->
                  ( A. x e. A ph /\ A. y e. A ps ) ) $=
      ( c0 wceq wa wral wb pm5.1 rzal jca sylan13 wn r19.3rzv birand r19.26
      syl6rbbr biraldv biland bitrd pm2.61i ) EFGZABHDEIZCEIZACEIZBDEIZHZJZUFUI
      UJUDUFUIKUECELUDUGUHACELBDELMNUDOZUFAUHHZCEIZUIUKUEULCEUKULADEIZUHHUEUKAU
      NUHADEPQABDERSTUKUIUGUHCEIZHUMUKUHUOUGUHCEPUAAUHCERSUBUC $.
      $( [11-Mar-97] $)
  $}



