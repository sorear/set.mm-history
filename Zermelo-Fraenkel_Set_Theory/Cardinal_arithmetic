$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +c $. $( Cardinal number addition $)
  $c .c $. $( Cardinal number multiplication $)
  $c ^c $. $( Cardinal number exponentiation $)

  $( Extend class definition to include cardinal number addition. $)
  ccda $a class +c $.

  $( Extend class definition to include cardinal number multiplication. $)
  ccdm $a class .c $.

  $( Extend class definition to include cardinal number exponentiation. $)
  ccde $a class ^c $.

  ${
    $d x y z $.
    $( Define cardinal number addition.  Definition of cardinal sum in
       [Stoll] p. 95.    See ~ cdavalt for its value.  For convenience, we
       extend its domain to all sets, not just cardinal numbers (but its
       value will always be a cardinal number).  Our definition is thus
       analogous to the definition of cardinal sum in [Mendelson] p. 258,
       except that we apply the ` card ` operation on the result. $)
    df-cda $a |- +c = { <. <. x , y >. , z >. |
                   z = ( card ` ( ( x X. { (/) } ) u. ( y X. { 1o } ) ) ) } $.

    $( Define cardinal number multiplication.  Definition of cardinal product
       in [Stoll] p. 95.  See ~ cdmvalt for its value.  See comment in
       ~ df-cda regarding the domain. $)
    df-cdm $a |- .c = { <. <. x , y >. , z >. |
                   z = ( card ` ( x X. y ) ) } $.

    $( Define cardinal number exponentiation.  Definition of cardinal power
       in [Stoll] p. 97.  See ~ cdevalt for its value.  See comment in
       ~ df-cda regarding the domain. $)
    df-cde $a |- ^c = { <. <. x , y >. , z >. |
                   z = ( card ` ( x ^m y ) ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95.  Normally the arguments ` A ` and ` B ` are expected
       to be cardinal numbers, but we allow arbitrary sets to make this
       theorem easier to use. $)
    cdavalt $p |- ( ( A e. C /\ B e. D ) ->  ( A +c B ) =
                    ( card ` ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) ) $=
      ( vx vy vz cvv wcel ccda co c0 csn cxp c1o cun ccrd cfv wceq cv fvex
      xpeq1 uneq1d fveq2d uneq2d copab2 wa df-cda visset pm3.2i biantrur
      bioprab eqtr oprabval2 elisset syl2an ) AHIBHIABJKALMZNZBOMZNZPZQRZSACIBD
      IEFGABHHETZUQNZFTZUSNZPZQRZVBJURVFPZQRVAQUAVCASZVGVIQVJVDURVFVCAUQUBUCUDV
      EBSZVIVAQVKVFUTURVEBUSUBUEUDJGTVHSZEFGUFVCHIZVEHIZUGZVLUGZEFGUFEFGUHVLVPE
      FGVOVLVMVNEUIFUIUJUKULUMUNACUOBDUOUP $.
      $( [28-Sep-04] $) $( [24-Sep-04] $)

    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95. $)
    cdmvalt $p |- ( ( A e. C /\ B e. D ) ->
                    ( A .c B ) = ( card ` ( A X. B ) ) ) $=
      ( vx vy vz cvv wcel ccdm co cxp ccrd cfv wceq cv fvex xpeq1 fveq2d xpeq2
      copab2 wa df-cdm visset pm3.2i biantrur bioprab eqtr oprabval2 elisset
      syl2an ) AHIBHIABJKABLZMNZOACIBDIEFGABHHEPZFPZLZMNZUMJAUOLZMNULMQUNAOUPUR
      MUNAUORSUOBOURULMUOBATSJGPUQOZEFGUAUNHIZUOHIZUBZUSUBZEFGUAEFGUCUSVCEFGVBU
      SUTVAEUDFUDUEUFUGUHUIACUJBDUJUK $.
      $( [?] $) $( [24-Sep-04] $)

    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95. $)
    cdevalt $p |- ( ( A e. C /\ B e. D ) ->
                    ( A ^c B ) = ( card ` ( A ^m B ) ) ) $=
      ( vx vy vz cvv wcel ccde co cm ccrd cfv wceq cv fvex opreq1 fveq2d
      opreq2 copab2 wa df-cde visset pm3.2i biantrur bioprab eqtr oprabval2
      elisset syl2an ) AHIBHIABJKABLKZMNZOACIBDIEFGABHHEPZFPZLKZMNZUMJAUOLKZMNU
      LMQUNAOUPURMUNAUOLRSUOBOURULMUOBALTSJGPUQOZEFGUAUNHIZUOHIZUBZUSUBZEFGUAEF
      GUCUSVCEFGVBUSUTVAEUDFUDUEUFUGUHUIACUJBDUJUK $.
      $( [?] $) $( [24-Sep-04] $)
  $}

  ${
    cdaval.1 $e |- A e. V $.
    cdaval.2 $e |- B e. V $.
    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95. $)
    cdaval $p |- ( A +c B ) =
                    ( card ` ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) $=
      ( cvv wcel ccda co c0 csn cxp c1o cun ccrd cfv wceq cdavalt mp2an ) AEF
      BEFABGHAIJKBLJKMNOPCDABEEQR $.
      $( [28-Sep-04] $) $( [24-Sep-04] $)

    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95. $)
    cdmval $p |- ( A .c B ) = ( card ` ( A X. B ) ) $=
      ( cvv wcel ccdm co cxp ccrd cfv wceq cdmvalt mp2an ) AEFBEFABGHABIJKLCD
      ABEEMN $.
      $( [?] $) $( [24-Sep-04] $)

    $( Value of cardinal addition.  Definition of cardinal sum in
       [Stoll] p. 95. $)
    cdeval $p |- ( A ^c B ) = ( card ` ( A ^m B ) ) $=
      ( cvv wcel ccde co cm ccrd cfv wceq cdevalt mp2an ) AEFBEFABGHABIHJKLCD
      ABEEMN $.
      $( [?] $) $( [24-Sep-04] $)
  $}

  ${
    cdaen.1 $e |- A e. V $.
    cdaen.2 $e |- B e. V $.
    cdaen.3 $e |- C e. V $.
    cdaen.4 $e |- D e. V $.
    $( Dardinal addition of equinumerous sets. $)
    cdaen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A +c C ) = ( B +c D ) ) $=
      ( cen wbr wa c0 csn cxp c1o cun ccrd cfv ccda co wceq cin wn 0ne1o
      xpsndisj ax-mp pm3.2i unen mpan2 cvv wcel wb 0ex xpsnen enen1 mp2an
      enen2 bitr con0 1o elisseti syl2anbr p0ex xpex snex unex carden sylibr
      cdaval 3eqtr4g ) ABIJZCDIJZKZALMZNZCOMZNZPZQRZBVNNZDVPNZPZQRZACSTBDSTVMVR
      WBIJZVSWCUAZVOVTIJZVQWAIJZWDVKVLWFWGKVOVQUBLUAZVTWAUBLUAZKWDWHWILOUAUCZWH
      UDALCOUEUFWJWIUDBLDOUEUFUGVOVTVQWAUHUIWFAVTIJZVKAUJUKVOAIJWFWKULEALEUMUNV
      OAVTUJUOUPBUJUKVTBIJWKVKULFBLFUMUNVTBAUJUQUPURWGCWAIJZVLCUJUKVQCIJWGWLULG
      COGOUSUTVAZUNVQCWAUJUOUPDUJUKWADIJWLVLULHDOHWMUNWADCUJUQUPURVBVRUJUKWBUJU
      KWEWDULVOVQAVNEVCVDCVPGOVEZVDVFVTWABVNFVCVDDVPHWNVDVFVRWBUJUJVGUPVHACEGVI
      BDFHVIVJ $.
      $( [?] $) $( [26-Sep-04] $)
  $}

  ${
    cda0.1 $e |- A e. V $.
    $( Cardinal addition with cardinal zero (the empty set). $)
    cda0 $p |- ( A +c (/) ) = ( card ` A ) $=
      ( c0 ccda co csn cxp c1o cun ccrd cfv 0ex cdaval wceq cen wbr xp0 uneq2i
      un0 eqtr xpsnen eqbrtr cvv wcel wb p0ex xpex snex unex carden mp2an
      mpbir ) ACDEACFZGZCHFZGZIZJKZAJKZACBLMURUSNZUQAOPZUQUNAOUQUNCIUNUPCUNUOQR
      UNSTACBLUAUBUQUCUDAUCUDUTVAUEUNUPAUMBUFUGCUOLHUHUGUIBUQAUCUCUJUKULT $.
      $( [?] $) $( [27-Sep-04] $)

    $( One times a cardinal number. $)
    cdm1 $p |- ( A .c 1o ) = ( card ` A ) $=
      ( c1o ccdm co c0 csn cxp ccrd cfv df1o2 opreq2i p0ex cdmval wceq cen wbr
      0ex xpsnen cvv wcel wb xpex carden mp2an mpbir 3eqtr ) ACDEAFGZDEAUHHZIJZ
      AIJZCUHADKLAUHBMNUJUKOZUIAPQZAFBRSUITUAATUAULUMUBAUHBMUCBUIATTUDUEUFUG $.
      $( [?] $) $( [27-Sep-04] $)

    $( Two times a cardinal number. $)
    cdtimes2 $p |- ( A .c 2o ) = ( A +c A ) $=
      ( c2o ccdm co c0 csn c1o cun ccda cpr df-pr df2o2 df1o2 sneqi uneq2i
      3eqtr4 opreq2i cxp ccrd cfv xpundi fveq2i p0ex snex unex cdmval cdaval
      eqtr ) ACDEAFGZHGZIZDEZAAJEZCULADFUJKUJUJGZICULFUJLMUKUOUJHUJNOPQRAULSZTU
      AAUJSAUKSIZTUAUMUNUPUQTAUJUKUBUCAULBUJUKUDHUEUFUGAABBUHQUI $.
      $( [?] $) $( [27-Sep-04] $)
  $}

  ${
    cdacom.1 $e |- A e. V $.
    cdacom.2 $e |- B e. V $.
    $( Commutative law for cardinal addition.  Theorem 6I(1) of [Enderton]
       p. 142. $)
    cdacom $p |- ( A +c B ) = ( B +c A ) $=
      ( c0 csn cxp c1o cun ccrd cfv ccda co wceq cen wbr cin wn 0ne1o xpsndisj
      ax-mp cleqcom mtbi wa 0ex xpsnen con0 1o elisseti entr4 pm3.2i unen mpan
      mp2an uncom breqtr cvv wcel wb p0ex xpex snex unex carden mpbir cdaval
      3eqtr4 ) AEFZGZBHFZGZIZJKZBVHGZAVJGZIZJKZABLMBALMVMVQNZVLVPOPZVLVOVNIZVPO
      VIVKQENZVOVNQENZVLVTOPZEHNZRWASAEBHTUAHENZRWBWDWESEHUBUCAHBETUAVIVOOPZVKV
      NOPZUDWAWBUDWCWFWGVIAVOCAECUEUFAHCHUGUHUIZUFUJVKBVNDBHDWHUFBEDUEUFUJUKVIV
      OVKVNULUMUNVOVNUOUPVLUQURVPUQURVRVSUSVIVKAVHCUTVABVJDHVBZVAVCVNVOBVHDUTVA
      AVJCWIVAVCVLVPUQUQVDUNVEABCDVFBADCVFVG $.
      $( [28-Sep-04] $) $( [24-Sep-04] $)

    $( Commutative law for cardinal multiplication.  Theorem 6I(1) of
       [Enderton] p. 142. $)
    cdmcom $p |- ( A .c B ) = ( B .c A ) $=
      ( cxp ccrd cfv ccdm co wceq cen wbr xpcomen cvv wcel wb xpex carden
      mp2an mpbir cdmval 3eqtr4 ) ABEZFGZBAEZFGZABHIBAHIUDUFJZUCUEKLZABCDMUCNOU
      ENOUGUHPABCDQBADCQUCUENNRSTABCDUABADCUAUB $.
      $( [?] $) $( [24-Sep-04] $)

    ${
      cdaass.3 $e |- C e. V $.
      $( Associative law for cardinal addition.  Theorem 6I(2) of [Enderton]
         p. 142. $)
      cdaass $p |- ( ( A +c B ) +c C ) = ( A +c ( B +c C ) ) $=
        ( c0 csn cxp c1o cun ccrd cfv ccda co wceq cen wbr wa cin cardid p0ex
        enref fvex xpex snex unex xpen mp2an xpundir breqtr con0 1o elisseti
        xpsnen ensymi pm3.2i wn 0ne1o xpsndisj ax-mp undisj1 mpbi unen xpassen
        xpcomen entr4 entr xpeq1 xpindir xp0 3eqtr3 unass 0ex undisj2 cvv wcel
        wb carden mpbir cdaval opreq1i eqtr opreq2i 3eqtr4 ) AGHZIZBJHZIZKZLMZW
        FIZCWHIZKZLMZWGBWFIZWMKZLMZWHIZKZLMZABNOZCNOZABCNOZNOZWOXAPZWNWTQRZWNWG
        WFIZWIWFIZKZWMWHIZKZWTWLXJQRZWMXKQRZSWLWMTGPZXJXKTGPZSWNXLQRXMXNWLWJWFI
        ZXJQWKWJQRWFWFQRWLXQQRWJUAWFUBUCWKWJWFWFWJLUDZWGWIAWFDUBUEZBWHEJUFZUEUG
        UBUBUHUIWGWIWFUJUKXKWMCWHFXTUEZWMJYAJULUMUNUOUPUQXOXPGJPURZXOUSWKGCJUTV
        AXHXKTGPZXIXKTGPZSXPYCYDYBYCUSWGGWMJUTVAZYBYDUSWIGWMJUTVAUQXHXIXKVBVCZU
        QWLXJWMXKVDUIXLXHWPWHIZXKKZKZWTXHYHWGWFXSUBUEZYGXKWPWHBWFEUBUEZXTUEWMWH
        YAXTUEZUGUGXLXHYGKZXKKZYIQXJYMQRZXKXKQRZSXPYMXKTGPZSXLYNQRYOYPXHXHQRZXI
        YGQRZSXHXITZGPZXHYGTGPZSYOYRYSXHYJUCXIBWHWFIZIZYGBWHWFEXTUBVEUVDBWFWHIZ
        IZYGBUVEEWFWHUBXTUEZUEBBQRUVCUVEQRUVDUVFQRBEUCWHWFXTUBVFBBUVCUVEEEWHWFX
        TUBUEUVGUHUIBWFWHEUBXTVEVGVHUQUVAUVBWGWITZWFIZGWFIZYTGUVHGPZUVIUVJPYBUV
        KUSAGBJUTVAUVHGWFVIVAWGWIWFVJWFVKVLYBUVBUSWGGWPJUTVAZUQXHXHXIYGVDUIXKYL
        UCUQXPYQYFYCYGXKTZGPZSYQYCUVNYEWPWMTZWHIZGWHIZUVMGUVOGPZUVPUVQPYBUVRUSB
        GCJUTVAUVOGWHVIVAWPWMWHVJWHVKVLUQXHYGXKVBVCUQXJYMXKXKVDUIXHYGXKVMUKWGXH
        QRZWSYHQRZSWGWSTGPZXHYHTGPZSWTYIQRUVSUVTXHWGXSWGGXSVNUOUPWSWQWHIZYHQWRW
        QQRWHWHQRWSUWCQRWQUAWHXTUCWRWQWHWHWQLUDZWPWMYKYAUGXTXTUHUIWPWMWHUJUKUQU
        WAUWBYBUWAUSAGWRJUTVAUVBYCSUWBUVBYCUVLYEUQXHYGXKVOVCUQWGXHWSYHVDUIVGVHW
        NVPVQWTVPVQXFXGVRWLWMWKWFXRUBUEYAUGWGWSXSWRWHUWDXTUEUGWNWTVPVPVSUIVTXCW
        KCNOWOXBWKCNABDEWAWBWKCXRFWAWCXEAWRNOXAXDWRANBCEFWAWDAWRDUWDWAWCWE $.
        $( [?] $) $( [26-Sep-04] $)

      $( Associative law for cardinal multiplication.  Theorem 6I(2) of
         [Enderton] p. 142. $)
      cdmass $p |- ( ( A .c B ) .c C ) = ( A .c ( B .c C ) ) $=
        ( cxp ccrd cfv ccdm co wceq cen wbr xpex cardid enref fvex xpen mp2an
        xpassen entr entr4 cvv wcel wb carden mpbir cdmval opreq1i eqtr
        opreq2i 3eqtr4 ) ABGZHIZCGZHIZABCGZHIZGZHIZABJKZCJKZABCJKZJKZUQVALZUPUT
        MNZUPAURGZUTAURDBCEFOZOUPUNCGZVHUOUNMNCCMNUPVJMNUNPCFQUOUNCCUNHRZABDEOF
        FSTABCDEFUAUBAAMNUSURMNUTVHMNADQURPAAUSURDDURHRZVISTUCUPUDUEUTUDUEVFVGU
        FUOCVKFOAUSDVLOUPUTUDUDUGTUHVCUOCJKUQVBUOCJABDEUIUJUOCVKFUIUKVEAUSJKVAV
        DUSAJBCEFUIULAUSDVLUIUKUM $.
        $( [?] $) $( [24-Sep-04] $)

      $( Cardinal multiplication distributes over cardinal addition.  Theorem
         6I(3) of [Enderton] p. 142. $)
      cdmadi $p |- ( A .c ( B +c C ) ) = ( ( A .c B ) +c ( A .c C ) ) $=
        ( c0 csn cxp c1o cun ccrd cfv ccda co ccdm wceq cen wbr p0ex xpex snex
        unex enref cardid fvex xpen mp2an wa cin xpassen entr pm3.2i wn 0ne1o
        xpsndisj ax-mp xpeq2 xpindi xp0r 3eqtr3 unen xpundi breqtrr entr4 cvv
        wcel wb carden mpbir cdaval opreq2i cdmval eqtr opreq12i 3eqtr4 ) ABGHZ
        IZCJHZIZKZLMZIZLMZABIZLMZVQIZACIZLMZVSIZKZLMZABCNOZPOZABPOZACPOZNOZWDWL
        QZWCWKRSZWCAWAIZWKAWADVRVTBVQETUACVSFJUBZUAUCZUAAARSWBWARSWCWTRSADUDWAU
        EAAWBWADDWALUFZXBUGUHWKAVRIZAVTIZKZWTRWGXDRSZWJXERSZUIWGWJUJGQZXDXEUJZG
        QZUIWKXFRSXGXHWGWEVQIZXDWFWERSVQVQRSWGXLRSWEUEVQTUDWFWEVQVQWELUFZABDEUA
        TTUGUHABVQDETUKULWJWHVSIZXEWIWHRSVSVSRSWJXNRSWHUEVSXAUDWIWHVSVSWHLUFZAC
        DFUAXAXAUGUHACVSDFXAUKULUMXIXKGJQUNZXIUOWFGWIJUPUQAVRVTUJZIZAGIZXJGXQGQ
        ZXRXSQXPXTUOBGCJUPUQXQGAURUQAVRVTUSAUTVAUMWGXDWJXEVBUHAVRVTVCVDVEWCVFVG
        WKVFVGWRWSVHAWBDXCUAWGWJWFVQXMTUAWIVSXOXAUAUCWCWKVFVFVIUHVJWNAWBPOWDWMW
        BAPBCEFVKVLAWBDXCVMVNWQWFWINOWLWOWFWPWINABDEVMACDFVMVOWFWIXMXOVKVNVP $.
        $( [?] $) $( [26-Sep-04] $)

      $( Sum of exponents law for cardinal arithmetic.  Theorem 6I(4) of
         [Enderton] p. 142. $)
      cdea $p |- ( A ^c ( B +c C ) ) = ( ( A ^c B ) .c ( A ^c C ) ) $=
        ( ccda co cm ccrd cfv ccde cxp ccdm wceq cen wbr oprex xpex c0 csn c1o
        cun enref cdaval cardid eqbrtr p0ex snex unex mapen mp2an cin wn 0ne1o
        xpsndisj ax-mp mapunen 0ex xpsnen con0 1o elisseti xpen entr cdeval
        entr4 cvv wcel wb carden mpbir cdmval 3eqtr4 ) ABCGHZIHZJKZABLHZACLHZMZ
        JKZAVOLHVRVSNHVQWAOZVPVTPQZVPABIHZACIHZMZVTWDWEABIRZACIRZSVPABTUAZMZCUB
        UAZMZUCZIHZWFAAPQZVOWMPQVPWNPQADUDZVOWMJKWMPBCEFUEWMUFUGAAVOWMDDBCGRZWJ
        WLBWIEUHSZCWKFUBUISZUJUKULWNAWJIHZAWLIHZMZWFWJWLUMTOZWNXBPQTUBOUNXCUOBT
        CUBUPUQWJWLAWRWSDURUQWTWDPQZXAWEPQZXBWFPQWOWJBPQXDWPBTEUSUTAAWJBDDWREUK
        ULWOWLCPQXEWPCUBFUBVAVBVCUTAAWLCDDWSFUKULWTWDXAWEAWJIRWGAWLIRWHVDULVEVE
        VRWDPQVSWEPQVTWFPQVRWDJKWDPABDEVFWDUFUGVSWEJKWEPACDFVFWEUFUGVRWDVSWEABL
        RZWGACLRZWHVDULVGVPVHVIVTVHVIWBWCVJAVOIRVRVSXFXGSVPVTVHVHVKULVLAVODWQVF
        VRVSXFXGVMVN $.
        $( [?] $) $( [27-Sep-04] $)

      $( Distributive law for cardinal exponentiation.  Theorem 6I(5) of
         [Enderton] p. 142. $)
      cdemdir $p |- ( ( A .c B ) ^c C ) = ( ( A ^c C ) .c ( B ^c C ) ) $=
        ( ccdm co cm ccrd cfv ccde cxp wceq cen wbr cdmval cardid eqbrtr enref
        oprex xpex mapen mp2an xpmapen cdeval xpen entr4 entr cvv wcel wb
        carden mpbir 3eqtr4 ) ABGHZCIHZJKZACLHZBCLHZMZJKZUPCLHUSUTGHURVBNZUQVAO
        PZUQABMZCIHZVAUPVEOPCCOPUQVFOPUPVEJKVEOABDEQVERSCFTUPVECCABGUAZABDEUBFF
        UCUDVFACIHZBCIHZMZVAVHVIACIUAZBCIUAZUBABCDEFUEUSVHOPUTVIOPVAVJOPUSVHJKV
        HOACDFUFVHRSUTVIJKVIOBCEFUFVIRSUSVHUTVIACLUAZVKBCLUAZVLUGUDUHUIUQUJUKVA
        UJUKVCVDULUPCIUAUSUTVMVNUBUQVAUJUJUMUDUNUPCVGFUFUSUTVMVNQUO $.
        $( [?] $) $( [27-Sep-04] $)

      $( Iterated exponent law for cardinal arithmetic.  Theorem 6I(6) of
         [Enderton] p. 142. $)
      cdee $p |- ( ( A ^c B ) ^c C ) = ( A ^c ( B .c C ) ) $=
        ( ccde co cm ccrd cfv ccdm wceq cen wbr cdeval cardid eqbrtr enref
        oprex mapen mp2an cxp mapxpen cdmval xpex entr4 entr cvv wcel wb
        carden mpbir 3eqtr4 ) ABGHZCIHZJKZABCLHZIHZJKZUOCGHAURGHUQUTMZUPUSNOZUP
        ABIHZCIHZUSUOVCNOCCNOUPVDNOUOVCJKVCNABDEPVCQRCFSUOVCCCABGTZABITFFUAUBVD
        ABCUCZIHZUSAVFITABCDEFUDAANOURVFNOUSVGNOADSURVFJKVFNBCEFUEVFQRAAURVFDDB
        CLTZBCEFUFUAUBUGUHUPUIUJUSUIUJVAVBUKUOCITAURITUPUSUIUIULUBUMUOCVEFPAURD
        VHPUN $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdale1 $p |- ( A (_ B -> ( A +c C ) (_ ( B +c C ) ) $=
        ( wss cdom wbr ccda co cvv wcel wi ssdomg ax-mp c0 csn cxp c1o cun
        ccrd cfv cen wb 0ex xpsnen domen1 mp2an domen2 bitr snex xpex domrefg
        wa cin wceq wn 0ne1o xpsndisj p0ex undom mpan2 sylbir unex carddom
        sylibr cdaval 3sstr4g syl ) ABGZABHIZACJKZBCJKZGALMZVKVLNDABLOPVLAQRZSZ
        CTRZSZUAZUBUCZBVPSZVSUAZUBUCZVMVNVLVTWCHIZWAWDGZVLVQWBHIZWEWGAWBHIZVLVO
        VQAUDIWGWHUEDAQDUFUGVQAWBLUHUIBLMWBBUDIWHVLUEEBQEUFUGWBBALUJUIUKWGVSVSH
        IZWEVSLMWICVRFTULUMZVSLUNPWGWIUOWBVSUPQUQZWEQTUQURWKUSBQCTUTPVQWBVSVSBV
        PEVAUMZWJWJVBVCVCVDVTLMWCLMWFWEUEVQVSAVPDVAUMWJVEWBVSWLWJVEVTWCLLVFUIVG
        ACDFVHBCEFVHVIVJ $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdale2 $p |- ( A (_ B -> ( C +c A ) (_ ( C +c B ) ) $=
        ( wss ccda co cdale1 cdacom 3sstr3g ) ABGACHIBCHICAHICBHIABCDEFJACDFKBC
        EFKL $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal multiplication.  Theorem 6L(b) of [Enderton]
         p. 149. $)
      cdmle1 $p |- ( A (_ B -> ( A .c C ) (_ ( B .c C ) ) $=
        ( wss cdom wbr ccdm co cvv wcel wi ssdomg ax-mp cxp ccrd cfv xpdom wb
        xpex carddom mp2an sylibr cdmcom cdmval eqtr 3sstr4g syl ) ABGZABHIZACJ
        KZBCJKZGALMUKULNDABLOPULCAQZRSZCBQZRSZUMUNULUOUQHIZUPURGZCABFDETUOLMUQL
        MUTUSUACAFDUBCBFEUBUOUQLLUCUDUEUMCAJKUPACDFUFCAFDUGUHUNCBJKURBCEFUFCBFE
        UGUHUIUJ $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal multiplication.  Theorem 6L(b) of [Enderton]
         p. 149. $)
      cdmle2 $p |- ( A (_ B -> ( C .c A ) (_ ( C .c B ) ) $=
        ( wss ccdm co cdmle1 cdmcom 3sstr3g ) ABGACHIBCHICAHICBHIABCDEFJACDFKBC
        EFKL $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal exponentiation.  Theorem 6L(c) of [Enderton]
         p. 149. $)
      cdele1 $p |- ( A (_ B -> ( A ^c C ) (_ ( B ^c C ) ) $=
        ( wss cdom wbr ccde co cvv wcel wi ssdomg ax-mp cm ccrd cfv mapdom1 wb
        oprex carddom mp2an sylibr cdeval 3sstr4g syl ) ABGZABHIZACJKZBCJKZGALM
        UIUJNDABLOPUJACQKZRSZBCQKZRSZUKULUJUMUOHIZUNUPGZABCDEFTUMLMUOLMURUQUAAC
        QUBBCQUBUMUOLLUCUDUEACDFUFBCEFUFUGUH $.
        $( [?] $) $( [27-Sep-04] $)

      $( Ordering law for cardinal exponentiation.  Theorem 6L(d) of [Enderton]
         p. 149. $)
      cdele2 $p |- ( ( A (_ B /\ -. ( A = (/) /\ C = (/) ) ) ->
                   ( C ^c A ) (_ ( C ^c B ) ) $=
        ( cdom wbr c0 wceq wa wn ccde co wss cm ccrd cfv mapdom2 cvv wcel wb
        oprex carddom mp2an sylibr cdeval 3sstr4g wi ssdomg ax-mp sylan ) ABGHZ
        AIJCIJKLZCAMNZCBMNZOABOZUMUNKZCAPNZQRZCBPNZQRZUOUPURUSVAGHZUTVBOZABCDEF
        SUSTUAVATUAVDVCUBCAPUCCBPUCUSVATTUDUEUFCAFDUGCBFEUGUHATUAUQUMUIDABTUJUK
        UL $.
        $( [?] $) $( [27-Sep-04] $)
    $}

    $( A cardinal is less than or equal to its cardinal sum with another. $)
    cdlea1 $p |- ( card ` A ) (_ ( A +c B ) $=
      ( ccrd cfv c0 ccda co cda0 wss 0ss 0ex cdale2 ax-mp eqsstr3 ) AEFAGHIZABH
      IZACJGBKQRKBLGBAMDCNOP $.
      $( [?] $) $( [27-Sep-04] $)

    $( A cardinal is less than or equal to its nonzero product with another. $)
    cdlem1 $p |- ( (/) e. B -> ( card ` A ) (_ ( A .c B ) ) $=
      ( c0 wcel csn wss ccrd cfv ccdm co snssi p0ex cdmle2 c1o cdm1 df1o2
      opreq2i eqtr3 syl5ss syl ) EBFEGZBHZAIJZABKLZHEBMUDAUCKLZUFUEUCBANDCOAPKL
      UEUGACQPUCAKRSTUAUB $.
      $( [?] $) $( [27-Sep-04] $)
  $}

