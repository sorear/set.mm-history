$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The universal class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the universal class. $)
  $c V $. $( The letter V $)

  $( Extend class notation to include the universal class symbol. $)
  cvv $a class V $.

  $( Define the universal class.  Definition 5.20, p. 21, of Takeuti and
     Zaring. $)
  df-v    $a |- V = { x | x = x } $.

  $( All set variables are sets (see ~ isset ).  Theorem 6.8 of Quine, p. 43. $)
  visset $p |- x e. V $=
    ( cv cvv wcel weq eqid df-v cleqabi mpbir ) ABCDAAEZAFJACAGHI $.
    $([5-Aug-93]$)

  ${
    $d x A $.
    $( Two ways to say " ` A ` is a set":  A class ` A ` is a member of
       the universal class ` V ` (see ~ df-v ) if and only if the class ` A `
       exists (i.e. there exists some set ` x ` equal to class ` A ` ).  We
       will use the notational device " ` A e. V ` " to mean "` A ` is a set"
       very frequently.  Note the when ` A ` is not a set, it is called a
       proper class.  Theorem 6.9 of Quine, p. 43. $)
    isset $p |- ( A e. V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wa wex df-clel visset biantru biex bitr4 ) BCDAEZBFZNC
      DZGZAHOAHABCIOQAPOAJKLM $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.
    isseti.1 $e |- A e. V $.
    $( A way to say "` A ` is a set" (inference rule). $)
    isseti   $p |- E. x x = A $=
      ( cvv wcel cv wceq wex isset mpbi ) BDEAFBGAHCABIJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.
    issetri.1 $e |- E. x x = A $.
    $( A way to say "` A ` is a set" (inference rule). $)
    issetri   $p |- A e. V $=
      ( cvv wcel cv wceq wex isset mpbir ) BDEAFBGAHCABIJ $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( If a class is a member of another class, it is a set.  Theorem 6.12
       of Quine, p. 44. $)
    elisset $p |- ( A e. B -> A e. V ) $=
      ( vx wcel cv wceq wex cvv wa df-clel 19.40 sylbi pm3.26d isset sylibr )
      ABDZCEZAFZCGZAHDPSQBDZCGZPRTICGSUAICABJRTCKLMCANO $.
      $([5-Aug-93]$)
  $}

  ${
    elisseti.1 $e |- A e. B $.
    $( If a class is a member of another class, it is a set. $)
    elisseti $p |- A e. V $=
      ( wcel cvv elisset ax-mp ) ABDAEDCABFG $.
      $([11-Jun-94]$)
  $}

  ${
    $d x A $.
    $( An element of a class exists. $)
    elex $p |- ( A e. B -> E. x x = A ) $=
      ( wcel cvv cv wceq wex elisset isset sylib ) BCDBEDAFBGAHBCIABJK $.
      $([1-May-95]$)
  $}

  ${
    $d x A $.
    ceqsal.1 $e |- ( ps -> A. x ps ) $.
    ceqsal.2 $e |- A e. V $.
    ceqsal.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An inference with implicit substitution. $)
    ceqsal $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( cv wceq wi wal wex isseti biimpd a2i 19.20i 19.23 sylib mpi biimprcd
      19.21ai impbi ) CHDIZAJZCKZBUEUCCLZBCDFMUEUCBJZCKUFBJUDUGCUCABUCABGNOPUCB
      CEQRSBUDCEUCABGTUAUB $.
      $([18-Aug-93]$)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsalv.1 $e |- A e. V $.
    ceqsalv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An inference with implicit substitution. $)
    ceqsalv $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( ax-17 ceqsal ) ABCDBCGEFH $.
      $([18-Aug-93]$)
  $}

  ${
    $d x ps $.
    gencl.1 $e |- ( th <-> E. x ( ch /\ A = B ) ) $.
    gencl.2 $e |- ( A = B -> ( ph <-> ps ) ) $.
    gencl.3 $e |- ( ch -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    gencl   $p |- ( th -> ps ) $=
      ( wceq wa wex syl5bi comm imp 19.23aiv sylbi ) DCFGKZLZEMBHTBECSBSCBSABCI
      JNOPQR $.
      $([17-May-96]$)
  $}

  ${
    $d x y $.  $d x R $.  $d x ps $.  $d y C $.  $d y S $.  $d y ch $.
    2gencl.1 $e |- ( C e. S <-> E. x ( x e. R /\ A = C ) ) $.
    2gencl.2 $e |- ( D e. S <-> E. y ( y e. R /\ B = D ) ) $.
    2gencl.3 $e |- ( A = C -> ( ph <-> ps ) ) $.
    2gencl.4 $e |- ( B = D -> ( ps <-> ch ) ) $.
    2gencl.5 $e |- ( ( x e. R /\ y e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    2gencl   $p |- ( ( C e. S /\ D e. S ) -> ch ) $=
      ( wcel wi cv wceq bilimd exp gencl comm imp ) HKQZIKQZCUGUFCUFBRUFCRESJQZ
      UGEGIMGITBCUFOUAUFUHBUHARUHBRDSJQZUFDFHLFHTABUHNUAUIUHAPUBUCUDUCUDUE $.
      $([17-May-96]$)
  $}

  ${
    $v F $.
    $v G $.
    3gencl.F $f class F $.
    3gencl.G $f class G $.
    $d x y z $.  $d y z D $.  $d z F $.  $d x y R $.  $d y z S $.
    $d x ps $.  $d y ch $.  $d z th $.
    3gencl.1 $e |- ( D e. S <-> E. x ( x e. R /\ A = D ) ) $.
    3gencl.2 $e |- ( F e. S <-> E. y ( y e. R /\ B = F ) ) $.
    3gencl.3 $e |- ( G e. S <-> E. z ( z e. R /\ C = G ) ) $.
    3gencl.4 $e |- ( A = D -> ( ph <-> ps ) ) $.
    3gencl.5 $e |- ( B = F -> ( ps <-> ch ) ) $.
    3gencl.6 $e |- ( C = G -> ( ch <-> th ) ) $.
    3gencl.7 $e |- ( ( x e. R /\ y e. R /\ z e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    3gencl   $p |- ( ( D e. S /\ F e. S /\ G e. S ) -> th ) $=
      ( wcel wi wa cv wceq bilimd 3exp imp 2gencl comm gencl exp 3imp ) KMUCZNM
      UCZOMUCZDUPUQURDUDURUPUQUEZDUSCUDUSDUDGUFLUCZURGJORJOUGCDUSUAUHUSUTCUTAUD
      ZUTBUDUTCUDEFHIKNLMPQHKUGABUTSUHINUGBCUTTUHEUFLUCZFUFLUCZVAVBVCUTAUBUIUJU
      KULUMULUNUO $.
      $([17-May-96]$)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    cgsex2g.1 $e |- ( ( x = A /\ y = B ) -> ch ) $.
    cgsex2g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsex2g   $p |- ( ( A e. C /\ B e. D ) ->
                     ( E. x E. y ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex wi biimpa 19.23aivv a1i biimprcd ancld 19.22dvv cv wceq
      elex im2an eeanv sylibr 19.22i syl syl5 comm impbid ) FHLZGILZMZCAMZENDNZ
      BUQBOUOUPBDECABKPQRBUOUQBCENZDNZUQUOBCUPDEBCACABKSTUAUODUBFUCZEUBGUCZMZEN
      ZDNZUSUOUTDNZVAENZMVDUMVEUNVFDFHUDEGIUDUEUTVADEUFUGVCURDVBCEJUHUHUIUJUKUL
      $.
      $([26-Jul-95]$)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $.
    cgsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) -> ch ) $.
    cgsex4g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 4 general classes. $)
    cgsex4g   $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                    ( E. x E. y E. z E. w ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex wi biimpa 19.23aivv a1i biimprcd ancld 19.22dvv cv wceq
      elex im2an eeanv sylibr ee4anv 19.22i syl syl5 comm impbid ) HLPZIMPZQZJL
      PZKMPZQZQZCAQZGRFRZERDRZBVGBSVDVFBDEVEBFGCABOTUAUAUBBVDVGBCGRZFRZERZDRZVG
      VDBVIVFDEBCVEFGBCACABOUCUDUEUEVDDUFHUGZEUFIUGZQZFUFJUGZGUFKUGZQZQZGRZFRZE
      RZDRZVKVDVNERDRZVQGRFRZQWBUTWCVCWDUTVLDRZVMERZQWCURWEUSWFDHLUHEIMUHUIVLVM
      DEUJUKVCVOFRZVPGRZQWDVAWGVBWHFJLUHGKMUHUIVOVPFGUJUKUIVNVQDEFGULUKWAVJDVTV
      IEVSVHFVRCGNUMUMUMUMUNUOUPUQ $.
      $([5-Aug-95]$)
  $}

  ${
    $d x A $.
    ceqsex.1 $e |- ( ps -> A. x ps ) $.
    ceqsex.2 $e |- A e. V $.
    ceqsex.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An inference with implicit substitution. $)
    ceqsex   $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( cv wceq wa wex biimpa 19.23ai isseti wi wal biimprcd 19.21ai exintr
      syl mpi impbi ) CHDIZAJZCKZBUDBCEUCABGLMBUCCKZUECDFNBUCAOZCPUFUEOBUGCEUCA
      BGQRUCACSTUAUB $.
      $([2-Mar-95]$)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsexv.1 $e |- A e. V $.
    ceqsexv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An inference with implicit substitution. $)
    ceqsexv   $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( ax-17 ceqsex ) ABCDBCGEFH $.
      $([2-Mar-95]$)
  $}
  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex.1 $e |- A e. V $.
    gencbvex.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbvex   $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex 19.11 wb bi2and bicomd cleqcoms ceqsexv biex anass
      pm5.32i ancom cleqcom biran 3bitr3 19.41v 3bitr 3bitr4r ) FLZGMZDBNZNZFOZ
      EOUOEOZFOCANZEOUNFOUOEFPUPUREUNURFGHUNURQGULGULMZURUNUSCDABJIRSTUAUBUQUNF
      UMEOZDNZBNUTUNNUNUQUTDBUCDVABDCUSNZEOUMDNZEOVAKVBVCEUSCNUSDNVBVCUSCDJUDUS
      CUEUSUMDGULUFUGUHUBUMDEUIUJUGUMUNEUIUKUBUH $.
      $([17-May-96]$)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbval.1 $e |- A e. V $.
    gencbval.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbval.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbval.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbval   $p |- ( A. x ( ch -> ph ) <-> A. y ( th -> ps ) ) $=
      ( wi wal wn wa wex cv wceq binegd gencbvex exnal 3bitr3 bicon4 ) CALEMZDB
      LFMZCANZOEPDBNZOFPUDNUENUFUGCDEFGHGFQRABISJKTCAEUADBFUAUBUC $.
      $([17-May-96]$)
  $}

  ${
    $d x A $.  $d x B $.
    clel2.1 $e |- A e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel2   $p |- ( A e. B <-> A. x ( x = A -> x e. B ) ) $=
      ( cv wceq wcel wi wal eleq1 ceqsalv bicom ) AEZBFMCGZHAIBCGZNOABDMBCJKL
      $.
      $([18-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.
    clel3.1 $e |- B e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3   $p |- ( A e. B <-> E. x ( x = B /\ A e. x ) ) $=
      ( cv wceq wcel wa wex eleq2 ceqsexv bicom ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $([18-Aug-93]$)
  $}

  ${
    $d x A $.  $d x B $.
    clel4.1 $e |- B e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel4   $p |- ( A e. B <-> A. x ( x = B -> A e. x ) ) $=
      ( cv wceq wcel wi wal eleq2 ceqsalv bicom ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $([18-Aug-93]$)
  $}

  ${
    $d x A $.
    vtocl.1 $e |- ( ps -> A. x ps ) $.
    vtocl.2 $e |- A e. V $.
    vtocl.3 $e |- ( x = A -> ( ph -> ps ) ) $.
    vtocl.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl   $p |- ps $=
      ( cv wceq wex wi isseti 19.22i ax-mp 19.36i mpg ) ABCABCECIDJZCKABLZCKCDF
      MRSCGNOPHQ $.
      $([25-Jun-98]$)
  $}

  ${
    $d x A $.
    vtoclb.1 $e |- ( ps -> A. x ps ) $.
    vtoclb.2 $e |- A e. V $.
    vtoclb.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclb.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclb   $p |- ps $=
      ( cv wceq biimpd vtocl ) ABCDEFCIDJABGKHL $.
      $([30-Aug-93]$)
  $}

  ${
    $d x A $.  $d x ps $.
    vtoclbv.1 $e |- A e. V $.
    vtoclbv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclbv.3 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclbv   $p |- ps $=
      ( ax-17 vtoclb ) ABCDBCHEFGI $.
      $([30-Aug-93]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    vtoclb2v.1 $e |- A e. V $.
    vtoclb2v.2 $e |- B e. V $.
    vtoclb2v.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    vtoclb2v.4 $e |- ph $.
    $( Implicit substitution of classes for set variables. $)
    vtoclb2v   $p |- ps $=
      ( wal wi wex cv wceq isseti wa eeanv biimpd 19.22i sylbir mp2an 19.36v
      biex bitr mpbi ax-gen mpg ) ADKZBCABLZDMZCMZUICKBLZCNEOZCMZDNFOZDMZULCEGP
      DFHPUOUQQUNUPQZDMZCMULUNUPCDRUSUKCURUJDURABISTTUAUBULUIBLZCMUMUKUTCABDUCU
      DUIBCUCUEUFADJUGUH $.
      $([26-Jul-95]$)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    vtoclb3v.1 $e |- A e. V $.
    vtoclb3v.2 $e |- B e. V $.
    vtoclb3v.3 $e |- C e. V $.
    vtoclb3v.4 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    vtoclb3v.5 $e |- ph $.
    $( Implicit substitution of classes for set variables. $)
    vtoclb3v   $p |- ps $=
      ( wal wi wex cv wceq isseti w3a eeeanv biimpd 19.22i sylbir mp3an 19.36v
      biex bitr mpbi gen2 mpg ) AENZDNZBCABOZEPZDPZCPZUMCNBOZCQFRZCPZDQGRZDPZEQ
      HRZEPZUQCFISDGJSEHKSUTVBVDTUSVAVCTZEPZDPZCPUQUSVAVCCDEUAVGUPCVFUODVEUNEVE
      ABLUBUCUCUCUDUEUQUMBOZCPURUPVHCUPULBOZDPVHUOVIDABEUFUGULBDUFUHUGUMBCUFUHU
      IADEMUJUK $.
      $([3-Jun-95]$)
  $}

  ${
    $d x A $.  $d x ch $. $d x th $.
    vtoclbbv.1 $e |- A e. V $.
    vtoclbbv.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclbbv.3 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclbbv.4 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclbbv   $p |- ( ch <-> th ) $=
      ( wb cv wceq bi2bid vtoclbv ) ABKCDKEFGELFMACBDHINJO $.
      $([23-Dec-93]$)
  $}

  ${
    $d x A $.
    vtoclgf.1 $e |- ( ps -> A. x ps ) $.
    vtoclgf.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgf.3 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclgf   $p |- ( A e. B -> ps ) $=
      ( wcel cvv elisset cv wceq wex isset mpbii 19.23ai sylbi syl ) DEIDJIZBDE
      KTCLDMZCNBCDOUABCFUAABHGPQRS $.
      $([17-Apr-95]$)
  $}

  ${
    $d x A $.  $d x ps $.
    vtoclg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclg.2 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclg   $p |- ( A e. B -> ps ) $=
      ( ax-17 vtoclgf ) ABCDEBCHFGI $.
      $([17-Apr-95]$)
  $}

  ${
    $d x A $.  $d x ch $. $d x th $.
    vtoclgb.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclgb.2 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclgb.3 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclgb   $p |- ( A e. B -> ( ch <-> th ) ) $=
      ( wb cv wceq bi2bid vtoclg ) ABKCDKEFGELFMACBDHINJO $.
      $([29-Apr-94]$)
  $}

  ${
    $d x A $.  $d y A $.  $d y B $.  $d y C $.
    vtocl2gf.1 $e |- ( ps -> A. x ps ) $.
    vtocl2gf.2 $e |- ( ch -> A. y ch ) $.
    vtocl2gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gf.5 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl2gf   $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( wcel wi ax-17 hbim cv wceq bilimd vtoclgf comm imp ) FHOZGIOZCUFUECUEBP
      UECPEGIUECEUEEQKRESGTBCUEMUAABDFHJLNUBUBUCUD $.
      $([25-Apr-95]$)
  $}

  ${
    $d x A $.  $d y A $.  $d y B $.  $d y C $.  $d x ps $.  $d y ch $.
    vtocl2i.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2i.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2i.3 $e |- ph $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2i   $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( ax-17 vtocl2gf ) ABCDEFGHIBDMCEMJKLN $.
      $([25-Apr-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclga.2 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclga   $p |- ( A e. B -> ps ) $=
      ( wcel cv wi wceq eleq1 bi2imd vtoclg pm2.43i ) DEHZBCIZEHZAJPBJCDEQDKRPA
      BQDELFMGNO $.
      $([20-Aug-95]$)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x ps $.  $d y ch $.
    vtocl2ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2ga.3 $e |- ( ( x e. C /\ y e. D ) -> ph ) $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2ga   $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( wcel wi cv wceq bilimd exp vtoclga comm imp ) FHMZGIMZCUCUBCUBBNUBCNEGI
      EOZGPBCUBKQUBUDIMZBUEANUEBNDFHDOZFPABUEJQUFHMUEALRSTSTUA $.
      $([20-Aug-95]$)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.
    $d x y z R $.  $d x y z S $.
    $d x ps $.  $d y ch $.  $d z th $.
    vtocl3ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3ga.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3ga.4 $e |- ( ( x e. D /\ y e. R /\ z e. S ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3ga   $p |- ( ( A e. D /\ B e. R /\ C e. S ) -> th ) $=
      ( wcel wi cv wceq bilimd wa 3exp imp3a vtoclga comm vtocl2ga exp com3r
      3imp ) HKRZILRZJMRZDUMUNULDUMUNULDSZULBSULCSUOFGIJLMFTZIUABCULOUBGTZJUACD
      ULPUBULUPLRZUQMRZUCZBUTASUTBSEHKETZHUAABUTNUBVAKRZURUSAVBURUSAQUDUEUFUGUH
      UIUJUK $.
      $([20-Aug-95]$)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocle.1 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocle   $p |- ( A e. V -> ph ) $=
      ( cvv wcel cv wceq wex isset 19.23aiv sylbi ) CEFBGCHZBIABCJMABDKL $.
      $([5-Aug-93]$)
  $}

  ${
    $d x A $.  $d x ph $.
    vtoclei.1 $e |- A e. V $.
    vtoclei.2 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclei   $p |- ph $=
      ( cvv wcel vtocle ax-mp ) CFGADABCEHI $.
      $([9-Sep-93]$)
  $}

  ${
    $d x A $.
    vtoclef.1 $e |- ( ph -> A. x ph ) $.
    vtoclef.2 $e |- A e. V $.
    vtoclef.3 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclef   $p |- ph $=
      ( cv wceq wex isseti 19.23ai ax-mp ) BGCHZBIABCEJMABDFKL $.
      $([18-Aug-93]$)
  $}

  ${
    $d x ps $.  $d x A $.  $d x B $.
    vtoclabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclabg.2 $e |- B = { x | ph } $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclabg   $p |- ( A e. C -> ( A e. B <-> ps ) ) $=
      ( cv wcel eleq1 cleqabi vtoclgb ) CIZEJADEJBCDFNDEKGACEHLM $.
      $([13-Sep-95]$)
  $}

  ${
    $d x ps $.  $d x A $.  $d x B $.
    vtoclab.1 $e |- A e. V $.
    vtoclab.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclab.3 $e |- B = { x | ph } $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclab   $p |- ( A e. B <-> ps ) $=
      ( cvv wcel wb vtoclabg ax-mp ) DIJDEJBKFABCDEIGHLM $.
      $([13-Sep-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclri.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclri.2 $e |- A. x e. B ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclri   $p |- ( A e. B -> ps ) $=
      ( rspec vtoclga ) ABCDEFACEGHI $.
      $([21-Nov-94]$)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x ps $.  $d y ch $.
    vtoclrd2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclrd2.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtoclrd2.3 $e |- A. x e. C A. y e. C ph $.
    $( Implicit substitution of a classes for restricted variables. $)
    vtoclrd2   $p |- ( ( A e. C /\ B e. C ) -> ch ) $=
      ( wcel wi cv wceq bilimd wral biraldv vtoclri r19.21be comm imp ) FHLZGHL
      ZCUDUCCUCBMUCCMEGHENGOBCUCJPUCBEHAEHQBEHQDFHDNFOABEHIRKSTSUAUB $.
      $([21-Nov-94]$)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.
    $d x ps $.  $d y ch $.  $d z th $.
    vtoclrd3.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclrd3.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtoclrd3.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtoclrd3.4 $e |- A. x e. D A. y e. D A. z e. D ph $.
    $( Implicit substitution of classes for restricted variables. $)
    vtoclrd3   $p |- ( ( A e. D /\ B e. D /\ C e. D ) -> th ) $=
      ( wcel wa wi cv wceq bilimd wral biraldv vtoclrd2 r19.21be vtoclri comm
      imp 3impa ) HKPZIKPZJKPZDUJUKQZULDULUMDUMCRUMDRGJKGSJTCDUMNUAUMCGKAGKUBBG
      KUBCGKUBEFHIKESHTABGKLUCFSITBCGKMUCOUDUEUFUGUHUI $.
      $([21-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclrbv.1 $e |- A e. B $.
    vtoclrbv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclrbv.3 $e |- A. x e. B ph $.
    $( Implicit substitution of a class for a restricted variable. $)
    vtoclrbv   $p |- ps $=
      ( wcel vtoclri ax-mp ) DEIBFABCDEGHJK $.
      $([21-Nov-94]$)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x ps $.  $d y ch $.
    vtoclrbv2.1 $e |- A e. C $.
    vtoclrbv2.2 $e |- B e. C $.
    vtoclrbv2.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclrbv2.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtoclrbv2.5 $e |- A. x e. C A. y e. C ph $.
    $( Implicit substitution of classes for restricted variables. $)
    vtoclrbv2   $p |- ch $=
      ( wcel vtoclrd2 mp2an ) FHNGHNCIJABCDEFGHKLMOP $.
      $([21-Nov-94]$)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.
    $d x ps $.  $d y ch $.  $d z th $.
    vtoclrbv3.1 $e |- A e. D $.
    vtoclrbv3.2 $e |- B e. D $.
    vtoclrbv3.3 $e |- C e. D $.
    vtoclrbv3.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclrbv3.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtoclrbv3.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtoclrbv3.7 $e |- A. x e. D A. y e. D A. z e. D ph $.
    $( Implicit substitution of classes for restricted variables. $)
    vtoclrbv3   $p |- th $=
      ( wcel vtoclrd3 mp3an ) HKSIKSJKSDLMNABCDEFGHIJKOPQRTUA $.
      $([21-Nov-94]$)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    $( The abstraction variable in an abstraction class is not free. $)
    hbabc $p |- ( A e. { x | ph } -> A. x A e. { x | ph } ) $=
      ( vy cab wcel wal cv wi wceq eleq1 bialdv bi2imd hbab1 vtoclg pm2.43i )
      CABEZFZRBGZDHZQFZUABGZIRSIDCQTCJZUARUBSTCQKZUCUARBUDLMABDNOP $.
      $([2-Jul-94]$)
  $}

  ${
    $d y A $.  $d x y z $.
    hblem.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Lemma for ~ hbeq and ~ hbel . $)
    hblem   $p |- ( z e. A -> A. x z e. A ) $=
      ( weq wex cv wcel wal wi a9e eleq1 syl6bir bialdv sylibd 19.23aiv ax-mp
      ) BCFZBGCHZDIZUAAJZKZBCLSUCBSUABHZDIZAJZUBSUAUEUFUDTDMZENSUEUAAUGOPQR $.
      $([5-Aug-93]$)
  $}

  ${
    $d y A w $.  $d z B w $.  $d x y w $.  $d x z w $.  $( ` w ` is dummy. $)
    hbeq.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbeq.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( If ` x ` is not free in ` A ` and ` B `, it is not free in ` A = B `. $)
    hbeq   $p |- ( A = B -> A. x A = B ) $=
      ( vw cv wcel wb wal wceq hblem hbbi hbal dfcleq bial 3imtr4 ) HIZDJZTEJZK
      ZHLZUDALDEMZUEALUCAHUAUBAABHDFNACHEGNOPHDEQZUEUDAUFRS $.
      $([5-Aug-93]$)
  $}

  ${
    $d y A w $.  $d z B w $.  $d x y w $.  $d x z w $.  $( ` w ` is dummy. $)
    hbel.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbel.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( If ` x ` is not free in ` A ` and ` B `, it is not free in
       ` A e. B `. $)
    hbel   $p |- ( A e. B -> A. x A e. B ) $=
      ( vw cv wceq wcel wa wex wal wel ax-17 hbeq hblem hban hbex df-clel bial
      3imtr4 ) HIZDJZUDEKZLZHMZUHANDEKZUIANUGAHUEUFAABBUDDBHOAPFQACHEGRSTHDEUAZ
      UIUHAUJUBUC $.
      $([5-Aug-93]$)
  $}

  ${
    $d x y z $.  $d y z A $.  $( ` z ` is dummy. $)
    cla4gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    cla4gf.2 $e |- ( ps -> A. x ps ) $.
    cla4gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution.  Compare Theorem 7.3
       of Quine, p. 44. $)
    cla4gf   $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( vz wcel cvv wal wi elisset wex cv wceq isset wel ax-17 hbeq cleq1
      cbvex bitr biimpd 19.22i sylbi 19.36 sylib syl ) EFKELKZACMBNZEFOULABNZCP
      ZUMULCQZERZCPZUOULJQZERZJPURJESUTUQJCCDDUSEDJTCUAGUBUQJUAUSUPEUCUDUEUQUNC
      UQABIUFUGUHABCHUIUJUK $.
      $([2-Feb-97]$)

    $( Existential specialization with implicit substitution. $)
    cla4egf   $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( wcel wn wal wex hbne cv wceq binegd cla4gf pm2.03d df-ex syl6ibr ) EFJZ
      BAKZCLZKACMUBUDBUCBKCDEFGBCHNCOEPABIQRSACTUA $.
      $([2-Feb-97]$)
  $}

  ${
    $d x ps $.  $d x y A $.  $( ` y ` is dummy. $)
    cla4gv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution.  Compare Theorem 7.3
       of Quine, p. 44. $)
    cla4gv   $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( vy cv wcel ax-17 cla4gf ) ABCGDEGHDICJBCJFK $.
      $([22-Jun-94]$)

    $( Existential specialization with implicit substitution. $)
    cla4egv   $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( vy cv wcel ax-17 cla4egf ) ABCGDEGHDICJBCJFK $.
      $([14-Aug-94]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2gv.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with implicit substitution. $)
    cla4e2gv   $p |- ( ( A e. C /\ B e. D ) -> ( ps -> E. x E. y ph ) ) $=
      ( wcel wa wex cv wceq biimprcd 19.22dvv elex im2an eeanv sylibr syl5
      comm ) BEGJZFHJZKZADLCLZBCMENZDMFNZKZDLCLZUFUEBUIACDUIABIOPUEUGCLZUHDLZKU
      JUCUKUDULCEGQDFHQRUGUHCDSTUAUB $.
      $([3-Aug-95]$)
  $}

  ${
    $d x A $.  $d x ps $.
    cla4v.1 $e |- A e. V $.
    cla4v.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution. $)
    cla4v $p |- ( A. x ph -> ps ) $=
      ( cvv wcel wal wi cla4gv ax-mp ) DGHACIBJEABCDGFKL $.
      $([22-Jun-94]$)

    $( Existential specialization with implicit substitution. $)
    cla4ev $p |- ( ps -> E. x ph ) $=
      ( wn wal wex cv wceq binegd cla4v con2 df-ex sylibr ) BAGZCHZGACIRBQBGCDE
      CJDKABFLMNACOP $.
      $([31-Dec-93]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rcla4v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization with implicit substitution. $)
    rcla4v $p |- ( A. x e. B ph -> ( A e. B -> ps ) ) $=
      ( wral cv wcel wi wal df-ral wceq eleq1 bi2imd cla4gv pm2.43b sylbi ) ACE
      GCHZEIZAJZCKZDEIZBJZACELUBUCBUAUDCDESDMTUCABSDENFOPQR $.
      $([26-May-98]$)

    $( Restricted existential specialization with implicit substitution. $)
    rcla4ev $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( wcel wa cv wex wrex wceq eleq1 bi2and cla4egv anabsi5 df-rex sylibr )
      DEGZBHZCIZEGZAHZCJZACEKSBUDUCTCDEUADLUBSABUADEMFNOPACEQR $.
      $([26-May-98]$)
  $}

  ${
    $d x y A $.  $d x C $.  $d x D $.  $d y B $.  $d y D $.  $d x ch $.
    $d y ps $.
    rcla42v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla42v.2 $e |- ( y = B -> ( ch <-> ps ) ) $.
    $( 2-variable restricted specialization with implicit substitution. $)
    rcla42v $p |- ( A. x e. C A. y e. D ph ->
                    ( ( A e. C /\ B e. D ) -> ps ) ) $=
      ( wral wcel wi wa cv wceq biraldv rcla4v syl3 imp3a syl ) AEILZDHLFHMZCEI
      LZNZUDGIMZOBNUCUEDFHDPFQACEIJRSUFUDUGBUEUGBNUDCBEGIKSTUAUB $.
      $([13-Sep-99]$)

    $( 2-variable restricted existential specialization with implicit
       substitution. $)
    rcla42ev $p |- ( ( ( A e. C /\ B e. D ) /\ ps ) ->
                   E. x e. C E. y e. D ph ) $=
      ( wcel wa wrex rcla4ev imlan exp32 imp31 cv wceq birexdv syl ) FHLZGILZMB
      MUCCEINZMZAEINZDHNUCUDBUFUCUDBUFUDBMUEUCCBEGIKOPQRUGUEDFHDSFTACEIJUAOUB
      $.
      $([16-Oct-99]$)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2v.1 $e |- A e. V $.
    cla4e2v.2 $e |- B e. V $.
    cla4e2v.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with implicit substitution. $)
    cla4e2v   $p |- ( ps -> E. x E. y ph ) $=
      ( cvv wcel wex wi cla4e2gv mp2an ) EJKFJKBADLCLMGHABCDEFJJINO $.
      $([3-Aug-95]$)
  $}

  ${
    $d x A y z $.  $d x B y z $.  $( ` y z ` are dummy. $)
    eqvinc.1 $e |- A e. V $.
    $( A variable introduction law for class equality. $)
    eqvinc $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( vz vy wceq cvv wcel cv wa wex eleq1 mpbii visset adantl 19.23aiv weq
      cleq2 biland biexdv cleq1 cleqcom syl6bb birand eqvin vtoclbbv vtoclgb
      pm5.21nii ) BCGZCHIZAJZBGZULCGZKZALZUJBHIUKDBCHMNUOUKAUNUKUMUNULHIUKAOULC
      HMNPQBEJZGZUMAERZKZALZUJUPECHUQCBSUQCGZUTUOAVBUSUNUMUQCULSTUAFERFARZUSKZA
      LURVAFBDFJZBUQUBVEBGZVDUTAVFVCUMUSVFVCBULGUMVEBULUBBULUCUDUEUAFEAUFUGUHUI
      $.
      $([14-Apr-95]$)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    alex.1 $e |- A e. V $.
    $( Two ways of expressing substitution of ` A ` for ` x ` in ` ph `. $)
    alex   $p |- ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) $=
      ( vy weq wi wal wa wex cv wceq cleq2 birimd bialdv birand biexdv eqs4
      eq5 hba1 ax-16 pm3.4 syl5 19.23ad eqs5 pm2.61i impbi vtoclbbv ) BEFZAGZBH
      ZUIAIZBJZBKZCLZAGZBHUOAIZBJECDEKZCLZUJUPBUSUIUOAURCUNMZNOUSULUQBUSUIUOAUT
      PQUKUMABERUIBHZUMUKGVAULUKBBEBSUJBTVAUJUKULUJBEUAUIAUBUCUDABEUEUFUGUH $.
      $([2-Mar-95]$)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( Equality implies equivalence with substitution. $)
    ceqsexg   $p |- ( x = A -> ( ph <-> E. x ( x = A /\ ph ) ) ) $=
      ( vy cvv wcel cv wceq wa wex wb 19.8a isset sylibr weq wi cleq2 birand
      biexdv bilbid bi2imd exp wal ax-4 comm visset alex syl5ibr impbid vtoclg
      mpcom ) CEFZBGZCHZAUNAIZBJZKZUNUNBJULUNBLBCMNBDOZAURAIZBJZKZPUNUQPDCEDGZC
      HZURUNVAUQVBCUMQZVCUTUPAVCUSUOBVCURUNAVDRSTUAURAUTURAUTUSBLUBURURAPZBUCZA
      UTVFURAVEBUDUEABVBDUFUGUHUIUJUK $.
      $([2-Mar-95]$)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsexvg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( An inference with implicit substitution. $)
    ceqsexvg   $p |- ( A e. B -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( wb cv wceq wa wex hbe1 ax-17 hbbi ceqsexg bi2bid pm4.2 vtoclgf ) AAGCHD
      IZAJZCKZBGCDEUABCTCLBCMNSAUAABACDOFPAQR $.
      $([29-Dec-96]$)
  $}

  ${
    $d ps y $.  $d x A y $.  $d y ph $.  $( ` y ` is dummy. $)
    abclf.1 $e |- ( ps -> A. x ps ) $.
    abclf.2 $e |- A e. V $.
    abclf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Implicit substitution for an element of an abstraction class. $)
    abclf   $p |- ( A e. { x | ph } <-> ps ) $=
      ( vy cab wcel wb cv ax-17 hbab1 hbel hbbi wceq eleq1 abid syl5bb bitr3d
      vtoclef ) DACIZJZBKCDUDBCCHHDUCHLDJCMACHNOEPFCLZDQZAUDBUFUEUCJUDAUEDUCRAC
      STGUAUB $.
      $([1-Aug-94]$)
  $}

  ${
    $d x ps $.  $d x A $.
    abcl.1 $e |- A e. V $.
    abcl.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Implicit substitution for an element of an abstraction class.
       Compare Theorem 6.13 of Quine, p. 44 $)
    abcl   $p |- ( A e. { x | ph } <-> ps ) $=
      ( ax-17 abclf ) ABCDBCGEFH $.
      $([1-Aug-94]$)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    abclg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Implicit substitution for an element of an abstraction class.
       Compare Theorem 6.13 of Quine, p. 44 $)
    abclg   $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv cab wcel wsb eleq1 wceq weq wa wex wb visset eqvinc hbsbv ax-17
      hbbi sbequ12 bicomd sylan9bb 19.23ai sylbi df-clab vtoclgb ) GHZACIZJACGK
      ZDUKJBGDEUJDUKLUJDMCGNZCHDMZOZCPULBQZCUJDGRSUOUPCULBCACGTBCUAUBUMULAUNBUM
      AULACGUCUDFUEUFUGAGCUHUI $.
      $([14-Apr-95]$)
  $}

  ${
    $d x ps $.  $d x A $.
    abcl2g.1 $e |- ( ps -> A e. V ) $.
    abcl2g.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Implicit substitution for an element of an abstraction class.
       Compare Theorem 6.13 of Quine, p. 44 $)
    abcl2g   $p |- ( A e. { x | ph } <-> ps ) $=
      ( cab wcel abclg biimpd pm2.43i cvv wb syl biimprd impbi ) DACGZHZBRBRRBA
      BCDQFIJKBRBRBBDLHRBMEABCDLFINOKP $.
      $([10-Nov-00]$)
  $}

  ${
    $d x ps $.  $d x A $.  $d x B $.
    rabcl.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Implicit substitution for an element of a restricted abstraction
       class. $)
    rabcl $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( crab wcel cvv wa elisset adantr cv cab wceq eleq1 bi2and abclg df-rab
      eleq2i syl5bb pm5.21nii ) DACEGZHZDIHZDEHZBJZDUCKUFUEBDEKLUEDCMZEHZAJZCNZ
      HUGUDUJUGCDIUHDOUIUFABUHDEPFQRUCUKDACESTUAUB $.
      $([21-May-99]$)
  $}

  ${
    $d x y z $.  $d z ph $.  $d z ps $.  $( ` z ` is dummy. $)
    cbvab.1 $e |- ( ph -> A. y ph ) $.
    cbvab.2 $e |- ( ps -> A. x ps ) $.
    cbvab.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvab   $p |- { x | ph } = { y | ps } $=
      ( vz cab wceq cv wcel wb hbab hbab1 cleqf visset abclf abid bitr mpgbir
      ) ACIZBDIZJDKZUBLZUDUCLZMDDHUBUCADCHENBDHOPUEBUFABCUDFDQGRBDSTUA $.
      $([8-Mar-95]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    cbvabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvabv   $p |- { x | ph } = { y | ps } $=
      ( ax-17 cbvab ) ABCDADFBCFEG $.
      $([26-May-99]$)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.
    cbvrabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvrabv   $p |- { x e. A | ph } = { y e. A | ps } $=
      ( cv wcel wa cab crab weq eleq1 bi2and cbvabv df-rab 3eqtr4 ) CGZEHZAIZCJ
      DGZEHZBIZDJACEKBDEKTUCCDCDLSUBABRUAEMFNOACEPBDEPQ $.
      $([26-May-99]$)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( Equality has existential uniqueness. $)
    eueq $p |- ( A e. V <-> E! x x = A ) $=
      ( vy cv wceq wex wa weq wi wal cvv wcel weu cleq2 biimparc gen2 biantru
      isset cleq1 eu4 3bitr4 ) ADZBEZAFZUDUCCDZBEZGACHZIZCJAJZGBKLUCAMUIUDUHACU
      FUGUCUEBUBNOPQABRUCUFACUBUEBSTUA $.
      $([25-Nov-94]$)
  $}

  ${
    $d x A $.
    eueq1.1 $e |- A e. V $.
    $( Equality has existential uniqueness. $)
    eueq1 $p |- E! x x = A $=
      ( cvv wcel cv wceq weu eueq mpbi ) BDEAFBGAHCABIJ $.
      $([5-Apr-95]$)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    eueq2.1 $e |- A e. V $.
    eueq2.2 $e |- B e. V $.
    $( Equality has existential uniqueness (split into 2 cases). $)
    eueq2 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ph /\ x = B ) ) $=
      ( cv wceq wa wn wo weu euorv negb eueq1 euanv biimpr mpan2 sylan13
      bianfd bilord orcom syl5bb bieudv mpbid mpian id birord pm2.61i ) AABGZCH
      ZIZAJZUJDHZIZKZBLZAUMULKZBLZUQUMJULBLZUSAUMULBMANZAUKBLZUTBCEOUTAVBIAUKBP
      QRSAURUPBAULUMKUPURAUMUOULAUMUNVATUAUMULUBUCUDUEUMAUOKZBLZUQUMUOBLZVDUMUN
      BLZVEBDFOVEUMVFIUMUNBPQRAUOBMUFUMVCUPBUMAULUOUMAUKUMUGTUHUDUEUI $.
      $([5-Apr-95]$)
  $}

  ${
    $d x ph $.  $d x ps $. $d x A $.  $d x B $. $d x C $.
    eueq3.1 $e |- A e. V $.
    eueq3.2 $e |- B e. V $.
    eueq3.3 $e |- C e. V $.
    eueq3.4 $e |- -. ( ph /\ ps ) $.
    $( Equality has existential uniqueness (split into 3 cases). $)
    eueq3 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( cv wceq wa wo wn w3o weu euorv orc con3 wi imnan mpbir con2 jaoi eueq1
      euanv biimpr mpan2 sylan13 negb orci bianfd bi2ord bilord orcom 3orass
      3bitr4g bieudv mpbid olc olci birord df-3or syl6bbr mpian syl6bb orass
      ecase3 ) ABACKZDLZMZABNZOZVJELZMZBVJFLZMZPZCQZAVNBNZVLNZCQZVTWAOVLCQZWCAW
      AVLCRWAAVNAOBAVMABSTZABABOZUAABMOJABUBUCZUDZUEUDAVKCQZWDCDGUFWDAWIMAVKCUG
      UHUIUJAWBVSCAVLWANVLVPVRNZNZWBVSAWAWJVLAVNVPBVRAVNVOABVNOZVMUKZULUMABVQWG
      UMUNUOWAVLUPVLVPVRUQZURUSUTBAVNNZVRNZCQZVTWOOVRCQZWQBWOVRCRWOBAWFVNWGBVMB
      AVATZUEUDBVQCQZWRCFIUFWRBWTMBVQCUGUHUIUJBWPVSCBWPVLVPNZVRNVSBWOXAVRBAVLVN
      VPBAVKWHUMBVNVOABWLWMVBUMUNVCVLVPVRVDVEUSUTVNVMVPNZCQZVTVNVPCQZXCVNVOCQZX
      DCEHUFXDVNXEMVNVOCUGUHUIVMVPCRVFVNXBVSCVNABVPNZNWKXBVSVNAVLXFWJVNAVKWEUMV
      NXFVRVPNWJVNBVRVPVNBVQWSUMVCVRVPUPVGUNABVPVHWNURUSUTVI $.
      $([5-Apr-95]$)
  $}

  ${
    $d x A $.
    $( There is at most one set equal to a class. $)
    moeq $p |- E* x x = A $=
      ( cv wceq wmo wex weu wi cvv wcel isset eueq bitr3 biimp df-mo mpbir ) AC
      BDZAEQAFZQAGZHRSRBIJSABKABLMNQAOP $.
      $([8-Mar-95]$)
  $}

  ${
    $d x y ph $.  $d x y ps $. $d x y A $.  $d x y B $. $d x y C $.
      $( ` y ` is dummy. $)
    moeq3.1 $e |- B e. V $.
    moeq3.2 $e |- C e. V $.
    moeq3.3 $e |- -. ( ph /\ ps ) $.
    $( "At most one" property of equality (split into 3 cases).  (The
       first 2 hypotheses could be eliminated with longer proof.) $)
    moeq3 $p |- E* x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( vy cvv wcel cv wceq wa wo wn w3o wmo weu weq cleq2 biland pm4.2i
      bi3ord bieudv visset eueq3 vtoclg eumo syl wi wal pm2.21 eleq1 mpbii
      syl5 imland imrord 3orass 3imtr4g 19.21aiv euimmo mpi pm2.61i ) DKLZACMZD
      NZOZABPQVGENOZBVGFNOZRZCSZVFVLCTZVMACJUAZOZVJVKRZCTZVNJDKJMZDNZVQVLCVTVPV
      IVJVJVKVKVTVOVHAVSDVGUBUCVTVJUDVTVKUDUEUFABCVSEFJUGGHIUHZUIVLCUJUKVFQZVRV
      MWAWBVLVQULZCUMVRVMULWBWCCWBVIVJVKPZPVPWDPVLVQWBVIVPWDWBVHVOAWBVFVOVHVFVO
      UNVHVGKLVFCUGVGDKUOUPUQURUSVIVJVKUTVPVJVKUTVAVBVLVQCVCUKVDVE $.
      $([23-Apr-95]$)
  $}

  ${
    $d x y A $.
    mosub.1 $e |- E* x ph $.
    $( "At most one" remains true after substitution. $)
    mosub   $p |- E* x E. y ( y = A /\ ph ) $=
      ( cv wceq wmo wal wa wex moeq ax-gen moexexv mp2an ) CFDGZCHABHZCIPAJCKBHC
      DLQCEMPACBNO $.
      $([9-Mar-95]$)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Theorem for inferring "at most one". $)
    mo2icl $p |- ( A. x ( ph -> x = A ) -> E* x ph ) $=
      ( vy cvv wcel cv wceq wi wal wmo weq cleq2 bilimd bialdv birimd wex
      19.8a ax-17 mo2 sylibr vtoclg wn visset eleq1 mpbii syl3 pm2.16d comm
      19.20dv 19.7 exmo ori sylbi syl6 pm2.61i ) CEFZABGZCHZIZBJZABKZIZABDLZIZB
      JZVBIVCDCEDGZCHZVFVAVBVHVEUTBVHVDUSAVGCURMNOPVFVFDQVBVFDRABDADSTUAUBUQUCZ
      VAAUCZBJZVBVIUTVJBUTVIVJUTAUQUSUQAUSUREFUQBUDURCEUEUFUGUHUIUJVKABQZUCVBAB
      UKVLVBABULUMUNUOUP $.
      $([17-Oct-96]$)
  $}

  ${
    $d x y $.
    $( The resolution of Russell's paradox:  there exists no set containing
       all sets which are not members of themselves.  Proposition 4.14
       of Takeuti and Zaring, p. 14. $)
    ru $p |- -. E. x x = { y | -. y e. y } $=
      ( cv wel wn cab wceq wb wal pm4.2 nbbn mpbi weq a13b a14b bitrd binegd
      bi2bid a4b1 mto cleqab mtbir nex ) ACZBBDZEZBFGZAUGBADZUFHZBIZUJAADZUKEZH
      ZULULHUMEULJUKULKLUIUMBABAMZUHUKUFULBAANUNUEUKUNUEABDUKBABNBAAOPQRSTUFBUD
      UAUBUC $.
      $([7-Aug-94]$)
  $}

