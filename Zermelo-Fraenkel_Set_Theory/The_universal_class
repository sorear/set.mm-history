$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The universal class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the universal class. $)
  $c V $. $( The letter V $)

  $( Extend class notation to include the universal class symbol. $)
  cvv $a class V $.

  $( Define the universal class.  Definition 5.20 of [TakeutiZaring] p. 21. $)
  df-v $a |- V = { x | x = x } $.

  $( All set variables are sets (see ~ isset ).  Theorem 6.8 of [Quine]
     p. 43. $)
  visset $p |- x e. V $=
    ( cv cvv wcel weq cleqid df-v cleqabi mpbir ) ABZCDAAEZJFKACAGHI $.
    $( [5-Aug-93] $)

  ${
    $d x A $.
    $( Two ways to say " ` A ` is a set":  A class ` A ` is a member of the
       universal class ` V ` (see ~ df-v ) if and only if the class ` A `
       exists (i.e. there exists some set ` x ` equal to class ` A ` ). Theorem
       6.9 of [Quine] p. 43.  _Notational convention_:  We will use the
       notational device " ` A e. V ` " to mean " ` A ` is a set" very
       frequently, for example in ~ uniex . Note the when ` A ` is not a set,
       it is called a proper class.  In some theorems, such as ~ uniexg , in
       order to shorten certain proofs we use the antecedent ` A e. B ` instead
       of ` A e. V ` to mean " ` A ` is a set".  $)
    isset $p |- ( A e. V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wa wex df-clel visset biantru biex bitr4 ) BCDAEZBFZNC
      DZGZAHOAHABCIOQAPOAJKLM $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.
    isseti.1 $e |- A e. V $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    isseti $p |- E. x x = A $=
      ( cvv wcel cv wceq wex isset mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.
    issetri.1 $e |- E. x x = A $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    issetri $p |- A e. V $=
      ( cvv wcel cv wceq wex isset mpbir ) BDEAFBGAHCABIJ $.
      $( [5-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( If a class is a member of another class, it is a set.  Theorem 6.12
       of [Quine] p. 44. $)
    elisset $p |- ( A e. B -> A e. V ) $=
      ( vx wcel cv wceq wex cvv wa df-clel 19.40 sylbi pm3.26d isset sylibr )
      ABDZCEZAFZCGZAHDPSQBDZCGZPRTICGSUAICABJRTCKLMCANO $.
      $( [5-Aug-93] $)
  $}

  ${
    elisseti.1 $e |- A e. B $.
    $( If a class is a member of another class, it is a set. $)
    elisseti $p |- A e. V $=
      ( wcel cvv elisset ax-mp ) ABDAEDCABFG $.
      $( [11-Jun-94] $)
  $}

  ${
    $d x A $.
    $( An element of a class exists. $)
    elex $p |- ( A e. B -> E. x x = A ) $=
      ( wcel cvv cv wceq wex elisset isset sylib ) BCDBEDAFBGAHBCIABJK $.
      $( [1-May-95] $)
  $}

  $( A universal quantifier restricted to the universe is unrestricted. $)
  ralv $p |- ( A. x e. V ph <-> A. x ph ) $=
    ( cvv wral cv wcel wi wal df-ral visset a1bi bial bitr4 ) ABCDBECFZAGZBHABH
    ABCIAOBNABJKLM $.
    $( [31-Mar-04] $) $( [26-Mar-04] $)

  $( An existential quantifier restricted to the universe is unrestricted. $)
  rexv $p |- ( E. x e. V ph <-> E. x ph ) $=
    ( cvv wrex cv wcel wa wex df-rex visset biantrur biex bitr4 ) ABCDBECFZAGZB
    HABHABCIAOBNABJKLM $.
    $( [4-Apr-04] $) $( [26-Mar-04] $)

  ${
    $d x y $. $d y A $.
    $( Commutation of restricted and unrestricted universal quantifiers. $)
    ralcom4 $p |- ( A. x e. A A. y ph <-> A. y A. x e. A ph ) $=
      ( wral cvv wal ralcom ralv biral 3bitr3r ) ABDEZCFEACFEZBDELCGACGZBDEACBF
      DHLCIMNBDACIJK $.
      $( [31-Mar-04] $) $( [26-Mar-04] $)

    $( Commutation of restricted and unrestricted existential quantifiers. $)
    rexcom4 $p |- ( E. x e. A E. y ph <-> E. y E. x e. A ph ) $=
      ( wrex cvv wex rexcom rexv birex 3bitr3r ) ABDEZCFEACFEZBDELCGACGZBDEACBF
      DHLCIMNBDACIJK $.
      $( [13-Apr-04] $) $( [12-Apr-04] $)
  $}

  ${
    $d x A $.
    ceqsalg.1 $e |- ( ps -> A. x ps ) $.
    ceqsalg.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsalg $p |- ( A e. B -> ( A. x ( x = A -> ph ) <-> ps ) ) $=
      ( wcel cv wceq wi wal wex biimpd a2i 19.20i 19.23 sylib elex syl5 com12
      biimprcd 19.21ai a1i impbid ) DEHZCIDJZAKZCLZBUIUFBUIUGCMZBUFUIUGBKZCLUJB
      KUHUKCUGABUGABGNOPUGBCFQRCDESTUABUIKUFBUHCFUGABGUBUCUDUE $.
      $( [29-Oct-03] $) $( [29-Oct-03] $)
  $}

  ${
    $d x A $.
    ceqsal.1 $e |- ( ps -> A. x ps ) $.
    ceqsal.2 $e |- A e. V $.
    ceqsal.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsal $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( cvv wcel cv wceq wi wal wb ceqsalg ax-mp ) DHICJDKALCMBNFABCDHEGOP $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsalv.1 $e |- A e. V $.
    ceqsalv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsalv $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( ax-17 ceqsal ) ABCDBCGEFH $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x ps $.
    gencl.1 $e |- ( th <-> E. x ( ch /\ A = B ) ) $.
    gencl.2 $e |- ( A = B -> ( ph <-> ps ) ) $.
    gencl.3 $e |- ( ch -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    gencl $p |- ( th -> ps ) $=
      ( wceq wa wex syl5bi com12 imp 19.23aiv sylbi ) DCFGKZLZEMBHTBECSBSCBSABC
      IJNOPQR $.
      $( [17-May-96] $)
  $}

  ${
    $d x y $.  $d x R $.  $d x ps $.  $d y C $.  $d y S $.  $d y ch $.
    2gencl.1 $e |- ( C e. S <-> E. x ( x e. R /\ A = C ) ) $.
    2gencl.2 $e |- ( D e. S <-> E. y ( y e. R /\ B = D ) ) $.
    2gencl.3 $e |- ( A = C -> ( ph <-> ps ) ) $.
    2gencl.4 $e |- ( B = D -> ( ps <-> ch ) ) $.
    2gencl.5 $e |- ( ( x e. R /\ y e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    2gencl $p |- ( ( C e. S /\ D e. S ) -> ch ) $=
      ( wcel wi cv wceq bilimd exp gencl com12 imp ) HKQZIKQZCUGUFCUFBRUFCRESJQ
      ZUGEGIMGITBCUFOUAUFUHBUHARUHBRDSJQZUFDFHLFHTABUHNUAUIUHAPUBUCUDUCUDUE $.
      $( [17-May-96] $)
  $}

  ${
    $v F $.
    $v G $.
    3gencl.F $f class F $.
    3gencl.G $f class G $.
    $d x y z $.  $d y z D $.  $d z F $.  $d x y R $.  $d y z S $.
    $d x ps $.  $d y ch $.  $d z th $.
    3gencl.1 $e |- ( D e. S <-> E. x ( x e. R /\ A = D ) ) $.
    3gencl.2 $e |- ( F e. S <-> E. y ( y e. R /\ B = F ) ) $.
    3gencl.3 $e |- ( G e. S <-> E. z ( z e. R /\ C = G ) ) $.
    3gencl.4 $e |- ( A = D -> ( ph <-> ps ) ) $.
    3gencl.5 $e |- ( B = F -> ( ps <-> ch ) ) $.
    3gencl.6 $e |- ( C = G -> ( ch <-> th ) ) $.
    3gencl.7 $e |- ( ( x e. R /\ y e. R /\ z e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    3gencl $p |- ( ( D e. S /\ F e. S /\ G e. S ) -> th ) $=
      ( wcel wi wa cv wceq bilimd 3exp imp 2gencl com12 gencl exp 3imp ) KMUCZN
      MUCZOMUCZDUPUQURDUDURUPUQUEZDUSCUDUSDUDGUFLUCZURGJORJOUGCDUSUAUHUSUTCUTAU
      DZUTBUDUTCUDEFHIKNLMPQHKUGABUTSUHINUGBCUTTUHEUFLUCZFUFLUCZVAVBVCUTAUBUIUJ
      UKULUMULUNUO $.
      $( [17-May-96] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    cgsex2g.1 $e |- ( ( x = A /\ y = B ) -> ch ) $.
    cgsex2g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsex2g $p |- ( ( A e. C /\ B e. D ) ->
                     ( E. x E. y ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex wi biimpa 19.23aivv a1i biimprcd ancld 19.22dvv cv wceq
      elex im2an eeanv sylibr 19.22i syl syl5 com12 impbid ) FHLZGILZMZCAMZENDN
      ZBUQBOUOUPBDECABKPQRBUOUQBCENZDNZUQUOBCUPDEBCACABKSTUAUODUBFUCZEUBGUCZMZE
      NZDNZUSUOUTDNZVAENZMVDUMVEUNVFDFHUDEGIUDUEUTVADEUFUGVCURDVBCEJUHUHUIUJUKU
      L $.
      $( [26-Jul-95] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $.
    cgsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) -> ch ) $.
    cgsex4g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 4 general classes. $)
    cgsex4g $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                    ( E. x E. y E. z E. w ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex wi biimpa 19.23aivv a1i biimprcd ancld 19.22dvv cv wceq
      elex im2an eeanv sylibr ee4anv 19.22i syl syl5 com12 impbid ) HLPZIMPZQZJ
      LPZKMPZQZQZCAQZGRFRZERDRZBVGBSVDVFBDEVEBFGCABOTUAUAUBBVDVGBCGRZFRZERZDRZV
      GVDBVIVFDEBCVEFGBCACABOUCUDUEUEVDDUFHUGZEUFIUGZQZFUFJUGZGUFKUGZQZQZGRZFRZ
      ERZDRZVKVDVNERDRZVQGRFRZQWBUTWCVCWDUTVLDRZVMERZQWCURWEUSWFDHLUHEIMUHUIVLV
      MDEUJUKVCVOFRZVPGRZQWDVAWGVBWHFJLUHGKMUHUIVOVPFGUJUKUIVNVQDEFGULUKWAVJDVT
      VIEVSVHFVRCGNUMUMUMUMUNUOUPUQ $.
      $( [5-Aug-95] $)
  $}

  ${
    $d x A $.
    ceqsex.1 $e |- ( ps -> A. x ps ) $.
    ceqsex.2 $e |- A e. V $.
    ceqsex.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsex $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( cv wceq wa wex biimpa 19.23ai isseti wi wal biimprcd 19.21ai exintr
      syl mpi impbi ) CHDIZAJZCKZBUDBCEUCABGLMBUCCKZUECDFNBUCAOZCPUFUEOBUGCEUCA
      BGQRUCACSTUAUB $.
      $( [2-Mar-95] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsexv.1 $e |- A e. V $.
    ceqsexv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexv $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( ax-17 ceqsex ) ABCDBCGEFH $.
      $( [2-Mar-95] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex.1 $e |- A e. V $.
    gencbvex.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbvex $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex excom wb bi2and bicomd cleqcoms ceqsexv biex anass
      pm5.32i ancom cleqcom biran 3bitr3 19.41v 3bitr 3bitr4r ) FLZGMZDBNZNZFOZ
      EOUOEOZFOCANZEOUNFOUOEFPUPUREUNURFGHUNURQGULGULMZURUNUSCDABJIRSTUAUBUQUNF
      UMEOZDNZBNUTUNNUNUQUTDBUCDVABDCUSNZEOUMDNZEOVAKVBVCEUSCNUSDNVBVCUSCDJUDUS
      CUEUSUMDGULUFUGUHUBUMDEUIUJUGUMUNEUIUKUBUH $.
      $( [17-May-96] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbval.1 $e |- A e. V $.
    gencbval.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbval.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbval.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbval $p |- ( A. x ( ch -> ph ) <-> A. y ( th -> ps ) ) $=
      ( wi wal wn wa wex cv wceq binegd gencbvex exanali 3bitr3 bicon4 ) CALEMZDB
      LFMZCANZOEPDBNZOFPUDNUENUFUGCDEFGHGFQRABISJKTCAEUADBFUAUBUC $.
      $( [17-May-96] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel2.1 $e |- A e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel2 $p |- ( A e. B <-> A. x ( x = A -> x e. B ) ) $=
      ( cv wceq wcel wi wal eleq1 ceqsalv bicom ) AEZBFMCGZHAIBCGZNOABDMBCJKL
      $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel3.1 $e |- B e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3 $p |- ( A e. B <-> E. x ( x = B /\ A e. x ) ) $=
      ( cv wceq wcel wa wex eleq2 ceqsexv bicom ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel4.1 $e |- B e. V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel4 $p |- ( A e. B <-> A. x ( x = B -> A e. x ) ) $=
      ( cv wceq wcel wi wal eleq2 ceqsalv bicom ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x A $.
    vtocl.1 $e |- ( ps -> A. x ps ) $.
    vtocl.2 $e |- A e. V $.
    vtocl.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable.  This is a
       generalization of ~ chv2 . $)
    vtoclf $p |- ps $=
      ( cv wceq wex wi isseti biimpd 19.22i ax-mp 19.36i mpg ) ABCABCECIDJZCKAB
      LZCKCDFMSTCSABGNOPQHR $.
      $( [30-Aug-93] $)
  $}

  ${
    $d x A $.  $d x ps $.
    vtocl.1 $e |- A e. V $.
    vtocl.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl.3 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl $p |- ps $=
      ( ax-17 vtoclf ) ABCDBCHEFGI $.
      $( [30-Aug-93] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    vtocl2.1 $e |- A e. V $.
    vtocl2.2 $e |- B e. V $.
    vtocl2.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    vtocl2.4 $e |- ph $.
    $( Implicit substitution of classes for set variables. $)
    vtocl2 $p |- ps $=
      ( wal wi wex cv wceq isseti wa eeanv biimpd 19.22i sylbir mp2an 19.36v
      biex bitr mpbi ax-gen mpg ) ADKZBCABLZDMZCMZUICKBLZCNEOZCMZDNFOZDMZULCEGP
      DFHPUOUQQUNUPQZDMZCMULUNUPCDRUSUKCURUJDURABISTTUAUBULUIBLZCMUMUKUTCABDUCU
      DUIBCUCUEUFADJUGUH $.
      $( [26-Jul-95] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    vtocl3.1 $e |- A e. V $.
    vtocl3.2 $e |- B e. V $.
    vtocl3.3 $e |- C e. V $.
    vtocl3.4 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    vtocl3.5 $e |- ph $.
    $( Implicit substitution of classes for set variables. $)
    vtocl3 $p |- ps $=
      ( wal wi wex cv wceq isseti w3a eeeanv biimpd 19.22i sylbir mp3an 19.36v
      biex bitr mpbi gen2 mpg ) AENZDNZBCABOZEPZDPZCPZUMCNBOZCQFRZCPZDQGRZDPZEQ
      HRZEPZUQCFISDGJSEHKSUTVBVDTUSVAVCTZEPZDPZCPUQUSVAVCCDEUAVGUPCVFUODVEUNEVE
      ABLUBUCUCUCUDUEUQUMBOZCPURUPVHCUPULBOZDPVHUOVIDABEUFUGULBDUFUHUGUMBCUFUHU
      IADEMUJUK $.
      $( [3-Jun-95] $)
  $}

  ${
    $d x A $.  $d x ch $. $d x th $.
    vtoclb.1 $e |- A e. V $.
    vtoclb.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclb.3 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclb.4 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclb $p |- ( ch <-> th ) $=
      ( wb cv wceq bi2bid vtocl ) ABKCDKEFGELFMACBDHINJO $.
      $( [23-Dec-93] $)
  $}

  ${
    $d y A $. $d x y $.
    vtoclgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgf.2 $e |- ( ps -> A. x ps ) $.
    vtoclgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgf.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable, with bound variable
       hypotheses in place of distinct variable restrictions. $)
    vtoclgf $p |- ( A e. B -> ps ) $=
      ( wcel cvv elisset cv wceq wex isset hbeleq ax-17 cleq1 cbvex bitr mpbii
      19.23ai sylbi syl ) EFKELKZBEFMUGCNZEOZCPZBUGDNZEOZDPUJDEQULUIDCCDEGRUIDS
      UKUHETUAUBUIBCHUIABJIUCUDUEUF $.
      $( [21-Sep-03] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    vtoclg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclg.2 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclg $p |- ( A e. B -> ps ) $=
      ( vy cv wcel ax-17 vtoclgf ) ABCHDEHIDJCKBCKFGL $.
      $( [17-Apr-95] $)
  $}

  ${
    $d x A $.  $d x ch $. $d x th $.
    vtoclbg.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclbg.2 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclbg.3 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclbg $p |- ( A e. B -> ( ch <-> th ) ) $=
      ( wb cv wceq bi2bid vtoclg ) ABKCDKEFGELFMACBDHINJO $.
      $( [29-Apr-94] $)
  $}

  ${
    $d x A $.  $d y z A $.  $d y z B $.  $d y C $.  $d z x $.
    vtocl2gf.1 $e |- ( ps -> A. x ps ) $.
    vtocl2gf.2 $e |- ( ch -> A. y ch ) $.
    vtocl2gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gf.5 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl2gf $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( vz wcel wi cv ax-17 hbim wceq bilimd vtoclgf com12 imp ) FHPZGIPZCUGUFC
      UFBQUFCQEOGIORZGPESUFCEUFESKTERGUABCUFMUBABDOFHUHFPDSJLNUCUCUDUE $.
      $( [25-Apr-95] $)
  $}

  ${
    $d x A $.  $d y A $.  $d y B $.  $d y C $.  $d x ps $.  $d y ch $.
    vtocl2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2g.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2g.3 $e |- ph $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2g $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( ax-17 vtocl2gf ) ABCDEFGHIBDMCEMJKLN $.
      $( [25-Apr-95] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclga.2 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclga $p |- ( A e. B -> ps ) $=
      ( wcel cv wi wceq eleq1 bi2imd vtoclg pm2.43i ) DEHZBCIZEHZAJPBJCDEQDKRPA
      BQDELFMGNO $.
      $( [20-Aug-95] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x ps $.  $d y ch $.
    vtocl2ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2ga.3 $e |- ( ( x e. C /\ y e. D ) -> ph ) $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2ga $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( wcel wi cv wceq bilimd exp vtoclga com12 imp ) FHMZGIMZCUCUBCUBBNUBCNEG
      IEOZGPBCUBKQUBUDIMZBUEANUEBNDFHDOZFPABUEJQUFHMUEALRSTSTUA $.
      $( [20-Aug-95] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.
    $d x y z R $.  $d x y z S $.
    $d x ps $.  $d y ch $.  $d z th $.
    vtocl3ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3ga.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3ga.4 $e |- ( ( x e. D /\ y e. R /\ z e. S ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3ga $p |- ( ( A e. D /\ B e. R /\ C e. S ) -> th ) $=
      ( wcel wi cv wceq bilimd wa 3exp imp3a vtoclga com12 vtocl2ga exp com3r
      3imp ) HKRZILRZJMRZDUMUNULDUMUNULDSZULBSULCSUOFGIJLMFTZIUABCULOUBGTZJUACD
      ULPUBULUPLRZUQMRZUCZBUTASUTBSEHKETZHUAABUTNUBVAKRZURUSAVBURUSAQUDUEUFUGUH
      UIUJUK $.
      $( [20-Aug-95] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocleg.1 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocleg $p |- ( A e. B -> ph ) $=
      ( wcel cv wceq wex elex 19.23aiv syl ) CDFBGCHZBIABCDJMABEKL $.
      $( [10-Jan-04] $) $( [10-Jan-04] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocle.1 $e |- A e. V $.
    vtocle.2 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocle $p |- ph $=
      ( cvv wcel vtocleg ax-mp ) CFGADABCFEHI $.
      $( [9-Sep-93] $)
  $}

  ${
    $d x A $.
    vtoclef.1 $e |- ( ph -> A. x ph ) $.
    vtoclef.2 $e |- A e. V $.
    vtoclef.3 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclef $p |- ph $=
      ( cv wceq wex isseti 19.23ai ax-mp ) BGCHZBIABCEJMABDFKL $.
      $( [18-Aug-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclri.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclri.2 $e |- A. x e. B ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclri $p |- ( A e. B -> ps ) $=
      ( rspec vtoclga ) ABCDEFACEGHI $.
      $( [21-Nov-94] $)
  $}

  ${
    $d x y $.  $d y A $.
    cla4gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    cla4gf.2 $e |- ( ps -> A. x ps ) $.
    cla4gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution.  Compare Theorem 7.3
       of [Quine] p. 44. $)
    cla4gf $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( wcel cvv wal wi elisset wex cv wceq isset hbeleq ax-17 cleq1 cbvex
      bitr biimpd 19.22i sylbi 19.36 sylib syl ) EFJEKJZACLBMZEFNUJABMZCOZUKUJC
      PZEQZCOZUMUJDPZEQZDOUPDERURUODCCDEGSUODTUQUNEUAUBUCUOULCUOABIUDUEUFABCHUG
      UHUI $.
      $( [2-Feb-97] $)

    $( Existential specialization with implicit substitution. $)
    cla4egf $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( wcel wn wal wex hbne cv wceq binegd cla4gf con2d df-ex syl6ibr ) EFJZBA
      KZCLZKACMUBUDBUCBKCDEFGBCHNCOEPABIQRSACTUA $.
      $( [2-Feb-97] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $( ` y ` is dummy. $)
    cla4gv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution.  Compare Theorem 7.3
       of [Quine] p. 44. $)
    cla4gv $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( vy cv wcel ax-17 cla4gf ) ABCGDEGHDICJBCJFK $.
      $( [22-Jun-94] $)

    $( Existential specialization with implicit substitution. $)
    cla4egv $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( vy cv wcel ax-17 cla4egf ) ABCGDEGHDICJBCJFK $.
      $( [14-Aug-94] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2gv.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with 2 quantifiers, using implicit
       substitution. $)
    cla4e2gv $p |- ( ( A e. C /\ B e. D ) -> ( ps -> E. x E. y ph ) ) $=
      ( wcel wa wex cv wceq biimprcd 19.22dvv elex im2an eeanv sylibr syl5
      com12 ) BEGJZFHJZKZADLCLZBCMENZDMFNZKZDLCLZUFUEBUIACDUIABIOPUEUGCLZUHDLZK
      UJUCUKUDULCEGQDFHQRUGUHCDSTUAUB $.
      $( [3-Aug-95] $)

    $( Specialization with 2 quantifiers, using implicit substitution. $)
    cla42gv $p |- ( ( A e. C /\ B e. D ) -> ( A. x A. y ph -> ps ) ) $=
      ( wcel wa wal wn wex cv wceq binegd cla4e2gv exnal biex bitr2 syl6ibr
      a3d ) EGJFHJKZBADLZCLZUDBMZAMZDNZCNZUFMZUHUGCDEFGHCOEPDOFPKABIQRUJUEMZCNU
      KUIULCADSTUECSUAUBUC $.
      $( [13-May-04] $) $( [27-Apr-04] $)
  $}

  ${
    $d x A $.  $d x ps $.
    cla4v.1 $e |- A e. V $.
    cla4v.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization with implicit substitution. $)
    cla4v $p |- ( A. x ph -> ps ) $=
      ( cvv wcel wal wi cla4gv ax-mp ) DGHACIBJEABCDGFKL $.
      $( [22-Jun-94] $)

    $( Existential specialization with implicit substitution. $)
    cla4ev $p |- ( ps -> E. x ph ) $=
      ( wn wal wex cv wceq binegd cla4v con2i df-ex sylibr ) BAGZCHZGACIRBQBGCD
      ECJDKABFLMNACOP $.
      $( [31-Dec-93] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rcla4v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization with implicit substitution. $)
    rcla4v $p |- ( A. x e. B ph -> ( A e. B -> ps ) ) $=
      ( wral cv wcel wi wal df-ral wceq eleq1 bi2imd cla4gv pm2.43b sylbi ) ACE
      GCHZEIZAJZCKZDEIZBJZACELUBUCBUAUDCDESDMTUCABSDENFOPQR $.
      $( [26-May-98] $)

    $( Restricted existential specialization with implicit substitution. $)
    rcla4ev $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( wcel wa cv wex wrex wceq eleq1 bi2and cla4egv anabsi5 df-rex sylibr )
      DEGZBHZCIZEGZAHZCJZACEKSBUDUCTCDEUADLUBSABUADEMFNOPACEQR $.
      $( [26-May-98] $)
  $}

  ${
    $d x y A $.  $d x C $.  $d x D $.  $d y B $.  $d y D $.  $d x ch $.
    $d y ps $.
    rcla42v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla42v.2 $e |- ( y = B -> ( ch <-> ps ) ) $.
    $( 2-variable restricted specialization with implicit substitution. $)
    rcla42v $p |- ( A. x e. C A. y e. D ph ->
                    ( ( A e. C /\ B e. D ) -> ps ) ) $=
      ( wral wcel wi wa cv wceq biraldv rcla4v syl3 imp3a syl ) AEILZDHLFHMZCEI
      LZNZUDGIMZOBNUCUEDFHDPFQACEIJRSUFUDUGBUEUGBNUDCBEGIKSTUAUB $.
      $( [13-Sep-99] $)

    $( 2-variable restricted existential specialization with implicit
       substitution. $)
    rcla42ev $p |- ( ( ( A e. C /\ B e. D ) /\ ps ) ->
                   E. x e. C E. y e. D ph ) $=
      ( wcel wa wrex rcla4ev imlan anassrs cv wceq birexdv syl ) FHLZGILZMBMUBC
      EINZMZAEINZDHNUBUCBUEUCBMUDUBCBEGIKOPQUFUDDFHDRFSACEIJTOUA $.
      $( [16-Oct-99] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2v.1 $e |- A e. V $.
    cla4e2v.2 $e |- B e. V $.
    cla4e2v.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with implicit substitution. $)
    cla4e2v $p |- ( ps -> E. x E. y ph ) $=
      ( cvv wcel wex wi cla4e2gv mp2an ) EJKFJKBADLCLMGHABCDEFJJINO $.
      $( [3-Aug-95] $)
  $}

  ${
    $d x A y z $.  $d x B y z $.  $( ` y z ` are dummy. $)
    eqvinc.1 $e |- A e. V $.
    $( A variable introduction law for class equality. $)
    eqvinc $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( vz vy wceq cvv wcel cv wa wex eleq1 mpbii visset adantl 19.23aiv weq
      cleq2 biland biexdv cleq1 cleqcom syl6bb birand eqvin vtoclb vtoclbg
      pm5.21nii ) BCGZCHIZAJZBGZULCGZKZALZUJBHIUKDBCHMNUOUKAUNUKUMUNULHIUKAOULC
      HMNPQBEJZGZUMAERZKZALZUJUPECHUQCBSUQCGZUTUOAVBUSUNUMUQCULSTUAFERFARZUSKZA
      LURVAFBDFJZBUQUBVEBGZVDUTAVFVCUMUSVFVCBULGUMVEBULUBBULUCUDUEUAFEAUFUGUHUI
      $.
      $( [14-Apr-95] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    eqvincf.1 $e |- ( y e. A -> A. x y e. A ) $.
    eqvincf.2 $e |- ( y e. B -> A. x y e. B ) $.
    eqvincf.3 $e |- A e. V $.
    $( A variable introduction law for class equality, requiring only that
       ` x ` not be free in ` A ` and ` B ` (instead of not occuring in
       them). $)
    eqvincf $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( wceq cv wa wex eqvinc hbeleq hban ax-17 weq cleq1 bi2and cbvex bitr )
      CDHBIZCHZUADHZJZBKAIZCHZUEDHZJZAKBCDGLUDUHBAUBUCAABCEMABDFMNUHBOBAPUBUFUC
      UGUAUECQUAUEDQRST $.
      $( [14-Sep-03] $)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    alexeq.1 $e |- A e. V $.
    $( Two ways of expressing substitution of ` A ` for ` x ` in ` ph ` . $)
    alexeq $p |- ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) $=
      ( vy weq wi wal wa wex cv wceq cleq2 birimd bialdv birand biexdv eqs4
      eq5 hba1 ax-16 pm3.4 syl5 19.23ad eqs5 pm2.61i impbi vtoclb ) BEFZAGZBHZU
      IAIZBJZBKZCLZAGZBHUOAIZBJECDEKZCLZUJUPBUSUIUOAURCUNMZNOUSULUQBUSUIUOAUTPQ
      UKUMABERUIBHZUMUKGVAULUKBBEBSUJBTVAUJUKULUJBEUAUIAUBUCUDABEUEUFUGUH $.
      $( [2-Mar-95] $)
  $}

  ${
    $d x A y $.  $d ph y $.  $( ` y ` is dummy. $)
    $( Equality implies equivalence with substitution. $)
    ceqsexg $p |- ( x = A -> ( ph <-> E. x ( x = A /\ ph ) ) ) $=
      ( vy cvv wcel cv wceq wa wex wb 19.8a isset sylibr weq wi cleq2 birand
      biexdv bilbid bi2imd exp wal ax-4 com12 visset alexeq syl5ibr impbid
      vtoclg mpcom ) CEFZBGZCHZAUNAIZBJZKZUNUNBJULUNBLBCMNBDOZAURAIZBJZKZPUNUQP
      DCEDGZCHZURUNVAUQVBCUMQZVCUTUPAVCUSUOBVCURUNAVDRSTUAURAUTURAUTUSBLUBURURA
      PZBUCZAUTVFURAVEBUDUEABVBDUFUGUHUIUJUK $.
      $( [2-Mar-95] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    ceqsexgv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexgv $p |- ( A e. B -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( vy wb cv wceq wa wex wcel ax-17 hbe1 hbbi ceqsexg bi2bid pm4.2 vtoclgf
      ) AAHCIDJZAKZCLZBHCGDEGIDMCNUCBCUBCOBCNPUAAUCABACDQFRAST $.
      $( [29-Dec-96] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    ceqsrexv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of a restricted existential quantifier, using implicit
       substitution. $)
    ceqsrexv $p |- ( A e. B -> ( E. x e. B ( x = A /\ ph ) <-> ps ) ) $=
      ( wcel cv wceq wa wex wrex eleq1 bi2and ceqsexgv ibar bitr4d df-rex an12
      biex bitr4 syl5bb ) DEGZCHZDIZUDEGZAJZJZCKZBUEAJZCELZUCUIUCBJZBUGULCDEUEU
      FUCABUDDEMFNOUCBPQUKUFUJJZCKUIUJCERUHUMCUEUFASTUAUB $.
      $( [13-May-04] $) $( [30-Apr-04] $)
  $}

  ${
    $d ps y $.  $d x A y $.  $d y ph $.  $( ` y ` is dummy. $)
    elabf.1 $e |- ( ps -> A. x ps ) $.
    elabf.2 $e |- A e. V $.
    elabf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in an abstraction class with implicit substitution. $)
    elabf $p |- ( A e. { x | ph } <-> ps ) $=
      ( vy cab wcel wb cv ax-17 hbab1 hbel hbbi wceq eleq1 abid syl5bbr bitr3d
      vtoclef ) DACIZJZBKCDUDBCCHHDUCHLDJCMACHNOEPFCLZDQZAUDBUFUEUCJUDAUEDUCRAC
      STGUAUB $.
      $( [1-Aug-94] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab.1 $e |- A e. V $.
    elab.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in an abstraction class with implicit substitution.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elab $p |- ( A e. { x | ph } <-> ps ) $=
      ( ax-17 elabf ) ABCDBCGEFH $.
      $( [1-Aug-94] $)
  $}

  ${
    $d y A $.  $d z ph $.  $d x y z $.
    elabgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elabgf.2 $e |- ( ps -> A. x ps ) $.
    elabgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in an abstraction class with implicit substitution.  Compare
       Theorem 6.13 of [Quine] p. 44.  This version has bound variable
       hypotheses in place of distinct variable restrictions. $)
    elabgf $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vz cv cab wcel wb hbab1 hbel hbbi wceq eleq1 bi2bid abid vtoclgf ) CKZA
      CLZMZANEUDMZBNCDEFGUFBCCDJEUDGACJOPHQUCERUEUFABUCEUDSITACUAUB $.
      $( [21-Sep-03] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    elabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in an abstraction class with implicit substitution.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elabg $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wcel ax-17 elabgf ) ABCGDEGHDICJBCJFK $.
      $( [14-Apr-95] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2g.2 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2g $p |- ( A e. C -> ( A e. B <-> ps ) ) $=
      ( wcel cab elabg eleq2i syl5bb ) DFIDACJZIBDEIABCDFGKENDHLM $.
      $( [12-Sep-04] $) $( [13-Sep-95] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2.1 $e |- A e. V $.
    elab2.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2.3 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2 $p |- ( A e. B <-> ps ) $=
      ( cvv wcel wb elab2g ax-mp ) DIJDEJBKFABCDEIGHLM $.
      $( [12-Sep-04] $) $( [13-Sep-95] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab3g.1 $e |- ( ps -> A e. V ) $.
    elab3g.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in an abstraction class using implicit substitution. $)
    elab3g $p |- ( A e. { x | ph } <-> ps ) $=
      ( cab wcel elabg ibi cvv wb syl biimprd pm2.43i impbi ) DACGZHZBRBABCDQFI
      JBRBRBBDKHRBLEABCDKFIMNOP $.
      $( [10-Nov-00] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.
    elrabf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elrabf.2 $e |- ( y e. B -> A. x y e. B ) $.
    elrabf.3 $e |- ( ps -> A. x ps ) $.
    elrabf.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted abstraction class with implicit substitution.
       This version has bound variable hypotheses in place of distinct variable
       restrictions. $)
    elrabf $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( crab wcel cvv wa elisset adantr cv cab hbel hban wceq eleq1 bi2and
      elabgf df-rab eleq2i syl5bb pm5.21nii ) EACFKZLZEMLZEFLZBNZEUIOULUKBEFOPU
      KECQZFLZANZCRZLUMUJUPUMCDEMGULBCCDDEFGHSITUNEUAUOULABUNEFUBJUCUDUIUQEACFU
      EUFUGUH $.
      $( [21-Sep-03] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $d x y B $.
    elrab.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted abstraction class with implicit
       substitution. $)
    elrab $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( vy cv wcel ax-17 elrabf ) ABCGDEGHZDICJLEICJBCJFK $.
      $( [21-May-99] $)
  $}

  ${
    $d x y z $.  $d z ph $.  $d z ps $.  $( ` z ` is dummy. $)
    cbvab.1 $e |- ( ph -> A. y ph ) $.
    cbvab.2 $e |- ( ps -> A. x ps ) $.
    cbvab.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvab $p |- { x | ph } = { y | ps } $=
      ( vz cab wceq cv wcel wb hbab hbab1 cleqf visset elabf abid bitr4 mpgbir
      ) ACIZBDIZJDKZUBLZUDUCLZMDDHUBUCADCHENBDHOPUEBUFABCUDFDQGRBDSTUA $.
      $( [8-Mar-95] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    cbvabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables with implicit substitution. $)
    cbvabv $p |- { x | ph } = { y | ps } $=
      ( ax-17 cbvab ) ABCDADFBCFEG $.
      $( [26-May-99] $)
  $}

  ${
    $d x y z $.  $d z A $.
    cbvrab.1 $e |- ( z e. A -> A. x z e. A ) $.
    cbvrab.2 $e |- ( z e. A -> A. y z e. A ) $.
    cbvrab.3 $e |- ( ph -> A. y ph ) $.
    cbvrab.4 $e |- ( ps -> A. x ps ) $.
    cbvrab.5 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted abstraction class,
       using implicit substitution.  This version has bound variable hypotheses
       in place of distinct variable conditions. $)
    cbvrab $p |- { x e. A | ph } = { y e. A | ps } $=
      ( cv wcel wa cab crab wel ax-17 hbel hban weq eleq1 bi2and cbvab df-rab
      3eqtr4 ) CLZFMZANZCODLZFMZBNZDOACFPBDFPUIULCDUHADDEEUGFECQDRHSITUKBCCEEUJ
      FEDQCRGSJTCDUAUHUKABUGUJFUBKUCUDACFUEBDFUEUF $.
      $( [26-Sep-03] $)
  $}

  ${
    $d x y z A $.  $d y ph $.  $d x ps $.
    cbvrabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted abstraction class,
       using implicit substitution. $)
    cbvrabv $p |- { x e. A | ph } = { y e. A | ps } $=
      ( vz cv wcel ax-17 cbvrab ) ABCDGEGHEIZCJLDJADJBCJFK $.
      $( [26-May-99] $)
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    $( Equality has existential uniqueness. $)
    eueq $p |- ( A e. V <-> E! x x = A ) $=
      ( vy cv wceq wex wa weq wi wal cvv wcel weu cleq2 biimparc gen2 biantru
      isset cleq1 eu4 3bitr4 ) ADZBEZAFZUDUCCDZBEZGACHZIZCJAJZGBKLUCAMUIUDUHACU
      FUGUCUEBUBNOPQABRUCUFACUBUEBSTUA $.
      $( [25-Nov-94] $)
  $}

  ${
    $d x A $.
    eueq1.1 $e |- A e. V $.
    $( Equality has existential uniqueness. $)
    eueq1 $p |- E! x x = A $=
      ( cvv wcel cv wceq weu eueq mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Apr-95] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    eueq2.1 $e |- A e. V $.
    eueq2.2 $e |- B e. V $.
    $( Equality has existential uniqueness (split into 2 cases). $)
    eueq2 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ph /\ x = B ) ) $=
      ( cv wceq wa wn wo weu euorv negb eueq1 euanv biimpr mpan2 sylan13
      bianfd bilord orcom syl5bb bieudv mpbid mpand id birord pm2.61i ) AABGZCH
      ZIZAJZUJDHZIZKZBLZAUMULKZBLZUQUMJULBLZUSAUMULBMANZAUKBLZUTBCEOUTAVBIAUKBP
      QRSAURUPBAULUMKUPURAUMUOULAUMUNVATUAUMULUBUCUDUEUMAUOKZBLZUQUMUOBLZVDUMUN
      BLZVEBDFOVEUMVFIUMUNBPQRAUOBMUFUMVCUPBUMAULUOUMAUKUMUGTUHUDUEUI $.
      $( [5-Apr-95] $)
  $}

  ${
    $d x ph $.  $d x ps $. $d x A $.  $d x B $. $d x C $.
    eueq3.1 $e |- A e. V $.
    eueq3.2 $e |- B e. V $.
    eueq3.3 $e |- C e. V $.
    eueq3.4 $e |- -. ( ph /\ ps ) $.
    $( Equality has existential uniqueness (split into 3 cases). $)
    eueq3 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( cv wceq wa wo wn w3o weu euorv orc con3i wi imnan mpbir con2i jaoi
      eueq1 euanv biimpr mpan2 sylan13 negb orci bianfd bi2ord bilord orcom
      3orass 3bitr4g bieudv mpbid olc olci birord df-3or syl6bbr mpand syl6bb
      orass ecase3 ) ABACKZDLZMZABNZOZVJELZMZBVJFLZMZPZCQZAVNBNZVLNZCQZVTWAOVLC
      QZWCAWAVLCRWAAVNAOBAVMABSTZABABOZUAABMOJABUBUCZUDZUEUDAVKCQZWDCDGUFWDAWIM
      AVKCUGUHUIUJAWBVSCAVLWANVLVPVRNZNZWBVSAWAWJVLAVNVPBVRAVNVOABVNOZVMUKZULUM
      ABVQWGUMUNUOWAVLUPVLVPVRUQZURUSUTBAVNNZVRNZCQZVTWOOVRCQZWQBWOVRCRWOBAWFVN
      WGBVMBAVATZUEUDBVQCQZWRCFIUFWRBWTMBVQCUGUHUIUJBWPVSCBWPVLVPNZVRNVSBWOXAVR
      BAVLVNVPBAVKWHUMBVNVOABWLWMVBUMUNVCVLVPVRVDVEUSUTVNVMVPNZCQZVTVNVPCQZXCVN
      VOCQZXDCEHUFXDVNXEMVNVOCUGUHUIVMVPCRVFVNXBVSCVNABVPNZNWKXBVSVNAVLXFWJVNAV
      KWEUMVNXFVRVPNWJVNBVRVPVNBVQWSUMVCVRVPUPVGUNABVPVHWNURUSUTVI $.
      $( [5-Apr-95] $)
  $}

  ${
    $d x A $.
    $( There is at most one set equal to a class. $)
    moeq $p |- E* x x = A $=
      ( cv wceq wmo wex weu wi cvv wcel isset eueq bitr3 biimp df-mo mpbir ) AC
      BDZAEQAFZQAGZHRSRBIJSABKABLMNQAOP $.
      $( [8-Mar-95] $)
  $}

  ${
    $d x y ph $.  $d x y ps $. $d x y A $.  $d x y B $. $d x y C $.
      $( ` y ` is dummy. $)
    moeq3.1 $e |- B e. V $.
    moeq3.2 $e |- C e. V $.
    moeq3.3 $e |- -. ( ph /\ ps ) $.
    $( "At most one" property of equality (split into 3 cases).  (The
       first 2 hypotheses could be eliminated with longer proof.) $)
    moeq3 $p |- E* x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( vy cvv wcel cv wceq wa wo wn w3o wmo weu weq cleq2 biland pm4.2i
      bi3ord bieudv visset eueq3 vtoclg eumo syl wi wal pm2.21 eleq1 mpbii
      syl5 imland imrord 3orass 3imtr4g 19.21aiv euimmo mpi pm2.61i ) DKLZACMZD
      NZOZABPQVGENOZBVGFNOZRZCSZVFVLCTZVMACJUAZOZVJVKRZCTZVNJDKJMZDNZVQVLCVTVPV
      IVJVJVKVKVTVOVHAVSDVGUBUCVTVJUDVTVKUDUEUFABCVSEFJUGGHIUHZUIVLCUJUKVFQZVRV
      MWAWBVLVQULZCUMVRVMULWBWCCWBVIVJVKPZPVPWDPVLVQWBVIVPWDWBVHVOAWBVFVOVHVFVO
      UNVHVGKLVFCUGVGDKUOUPUQURUSVIVJVKUTVPVJVKUTVAVBVLVQCVCUKVDVE $.
      $( [23-Apr-95] $)
  $}

  ${
    $d x y A $.
    mosub.1 $e |- E* x ph $.
    $( "At most one" remains true after substitution. $)
    mosub $p |- E* x E. y ( y = A /\ ph ) $=
      ( cv wceq wmo wal wa wex moeq ax-gen moexexv mp2an ) CFDGZCHABHZCIPAJCKBH
      CDLQCEMPACBNO $.
      $( [9-Mar-95] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $( ` y ` is dummy. $)
    $( Theorem for inferring "at most one". $)
    mo2icl $p |- ( A. x ( ph -> x = A ) -> E* x ph ) $=
      ( vy cvv wcel cv wceq wi wal wmo weq cleq2 bilimd bialdv birimd wex
      19.8a ax-17 mo2 sylibr vtoclg wn visset eleq1 mpbii syl3 con3d com12
      19.20dv alnex exmo ori sylbi syl6 pm2.61i ) CEFZABGZCHZIZBJZABKZIZABDLZIZB
      JZVBIVCDCEDGZCHZVFVAVBVHVEUTBVHVDUSAVGCURMNOPVFVFDQVBVFDRABDADSTUAUBUQUCZ
      VAAUCZBJZVBVIUTVJBUTVIVJUTAUQUSUQAUSUREFUQBUDURCEUEUFUGUHUIUJVKABQZUCVBAB
      UKVLVBABULUMUNUOUP $.
      $( [17-Oct-96] $)
  $}

  ${
    $d x y $.
    $( Russell's Paradox.  Proposition 4.14 of [TakeutiZaring] p. 14.  Frege's
       Axiom of (unrestricted) Comprehension, expressed in our notation as
       ` A e. V ` , asserted that any collection of sets ` A ` is a set i.e.
       belongs to the universe ` V ` of all sets.  In particular, by
       substituting ` { x | x e/ x } ` for ` A ` , it asserted
       ` { x | x e/ x } e. V ` , meaning that the "collection of all sets which
       are not members of themselves" is a set.  However, here we prove
       ` { x | x e/ x } e/ V ` . This contradiction was discovered by Russell
       in 1901 (published in 1903), invalidating Comprehension and leading to
       the collapse of Frege's system.  In 1908 Zermelo rectified this fatal
       flaw by replacing Comprehension with a weaker Subset (or Separation)
       Axiom ~ ssex asserting that ` A ` is a set only when it is smaller than
       some other set ` B ` . However, Zermelo was then faced with a "chicken
       and egg" problem of how to show ` B ` is a set, leading him to introduce
       the set-building axioms of Null Set ~ 0ex , Pairing ~ prex , Union
       ~ uniex , Power Set ~ pwex , and Infinity ~ omex to give him some
       starting sets to work with (all of which, before Russell's Paradox, were
       immediate consequences of Frege's Comprehension).  In 1922 Fraenkel
       strengthened the Subset Axiom with our present Replacement Axiom
       ~ funimaex (whose modern formalization is due to Skolem, also in 1922).
       Thus in a very real sense Russell's Paradox spawned the invention of ZF
       set theory and completely revised the foundations of mathematics!

       Another mainstream formalization of set theory, devised by von
       Neumann, Bernays, and Goedel, uses class variables rather than set
       variables as its primitives.  The axiom system NBG in [Mendelson]
       p. 225 is suitable for a Metamath encoding.  NBG is a conservative
       extension of ZF in that it proves exactly the same theorems as ZF that
       are expressable in the language of ZF.  An advantage of NBG is that it
       is finitely axiomatizable - the Axiom of Replacement can be broken down
       into a finite set of formulas that eliminate its wff metavariable.
       Finite axiomatizability is required by some proof languages (although
       not by Metamath).  There is a stronger version of NBG called
       Morse-Kelley (axiom system MK in [Mendelson] p. 287).

       Russell himself continued in a different direction, avoiding the paradox
       with his "theory of types."  Quine extended Russell's ideas to formulate
       the very strong New Foundations set theory (axiom system NF of [Quine]
       p. 331).  In NF the set of all sets is a set, contradicting ZF and
       NBG set theories, and it has other bizarre consequences:  when sets
       become too huge (beyond the size of those used in standard mathematics),
       the Axiom of Choice ~ ac4 and Cantor's Theorem ~ canth2 are provably
       false!  Nonetheless NF has not been shown to be inconsistent and has its
       advocates - who's to say which set theory is "right"?  NF is finitely
       axiomatizable and can be encoded in Metamath using the axioms from T.
       Hailperin, "A set of axioms for logic," _J. Symb. Logic_ 9:1-19
       (1944). $)
    ru $p |- { x | x e/ x } e/ V $=
      ( vy cv wnel cab cvv wcel wn wceq wex wel wb wal pm4.2 pm5.18 mpbi weq
      eleq1 id eleq12d binegd df-nel syl5bb bi2bid a4b1 mto cleqab mtbir nex
      isset mpbir ) ACZULDZAEZFDUNFGZHUOBCZUNIZBJUQBUQABKZUMLZAMZUTBBKZVAHZLZVA
      VALVCHVANVAVAOPUSVCABABQZURVAUMVBULUPUPRVDAAKZHVBUMVDVEVAVDULUPULUPVDSZVF
      TUAULULUBUCUDUEUFUMAUPUGUHUIBUNUJUHUNFUBUK $.
      $( [7-Aug-94] $)
  $}

