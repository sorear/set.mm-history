$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Miscellaneous theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Multiplication of both sides of 'less than or equal to' by a nonnegative
       number. $)
    lemul2itALT $p |- ( ( A e. RR /\ B e. RR /\ ( C e. RR /\ 0 <_ C ) ) ->
                        ( A <_ B -> ( C x. A ) <_ ( C x. B ) ) ) $=
      ( cr wcel cc0 cle wbr wa w3a cmul co lemul2it ex ) ADEBDECDEFCGHIJABGHCAK
      LCBKLGHABCMN $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    $( Division of both sides of 'less than or equal to' by a nonnegative
       number. $)
    lediv2itALT $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. RR /\ 0 < B ) /\
                       ( C e. RR /\ 0 <_ C ) ) ->
                     ( A <_ B -> ( C / B ) <_ ( C / A ) ) ) $=
      ( cr wcel cc0 clt wbr wa cle w3a c1 cdiv co cmul wi wne gt0ne0t rerecclt
      syldan anim12i ancoms 3adant3 3simp3 jca df-3an sylibr lemul2itALT syl
      wb lerect wceq cc recnt adantr 3anass divrect 3adant1 3adant2 breq12d
      3imtr4d ) ADEZFAGHZIZBDEZFBGHZIZCDEZFCJHZIZKZLBMNZLAMNZJHZCVLONZCVMONZJHZ
      ABJHZCBMNZCAMNZJHVKVLDEZVMDEZVJKZVNVQPVKWAWBIZVJIWCVKWDVJVDVGWDVJVGVDWDVG
      WAVDWBVEVFBFQZWABRZBSTVBVCAFQZWBARZASTUAUBUCVDVGVJUDUEWAWBVJUFUGVLVMCUHUI
      VDVGVRVNUJVJABUKUCVKVSVOVTVPJVGVJVSVOULZVDVJVGWIVJVGIZCUMEZBUMEZWEKZWIWJW
      KWLWEIZIWMVJWKVGWNVHWKVICUNUOZVGWLWEVEWLVFBUNUOWFUEUAWKWLWEUPUGCBUQUIUBUR
      VDVJVTVPULZVGVJVDWPVJVDIZWKAUMEZWGKZWPWQWKWRWGIZIWSVJWKVDWTWOVDWRWGVBWRVC
      AUNUOWHUEUAWKWRWGUPUGCAUQUIUBUSUTVA $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    abs2sqle.1 $e |- A e. CC $.
    abs2sqle.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqle $p |- ( ( abs ` A ) <_ ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr c2 cexp co wb absge0 abscl le2sq mp2an ) EAFGZHIEB
      FGZHIRSHIRJKLSJKLHIMACNBDNRSACOBDOPQ $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    abs2sqlt.1 $e |- A e. CC $.
    abs2sqlt.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqlt $p |- ( ( abs ` A ) < ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr clt c2 cexp co wb absge0 abscl lt2sq mp2an ) EAFGZ
      HIEBFGZHISTJISKLMTKLMJINACOBDOSTACPBDPQR $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqlet $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) <_ ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv cle wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqle dedth2h ) ACDZBCDZA
      EFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJK
      ZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSVD
      VGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUEU
      F $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqltt $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) < ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv clt wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqlt dedth2h ) ACDZBCDZA
      EFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJK
      ZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSVD
      VGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUEU
      F $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    abs2dif.1 $e |- A e. CC $.
    abs2dif.2 $e |- B e. CC $.
    $( Difference of absolute values. $)
    abs2dif $p |- ( ( abs ` A ) - ( abs ` B ) ) <_ ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2dift mp2an ) AEFBEFAGHBGHIJABIJGHK
      LCDABMN $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

  ${
    abs2difabs.1 $e |- A e. CC $.
    abs2difabs.2 $e |- B e. CC $.
    $( Absolute value of difference of absolute values. $)
    abs2difabs $p |- ( abs ` ( ( abs ` A ) - ( abs ` B ) ) ) <_
                     ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2difabst mp2an ) AEFBEFAGHBGHIJGHAB
      IJGHKLCDABMN $.
      $( [7-Sep-2007] $) $( [7-Sep-2007] $)
  $}

