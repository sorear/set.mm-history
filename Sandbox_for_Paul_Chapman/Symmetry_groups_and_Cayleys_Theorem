$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SymGrp $.

  $( Extend class notation to include the class of symmetry groups. $)
  csymgrp $a class SymGrp $.

  ${
    $d x y f g h $.
    $( Define the symmetry group on set ` x ` .  We represent the group as the
       set of 1-1-onto functions from ` x ` to itself under function
       composition. $)
    df-symgrp $a |- SymGrp = { <. x , y >. | y = { <. <. f , g >. , h >. |
      ( f : x -1-1-onto-> x /\ g : x -1-1-onto-> x /\ h = ( f o. g ) ) } } $.
  $}

  ${
    $d A f g h x $.  $d F f g h $.  $d F x $.  $d G f g h $.  $d P f g h $.
    $d f g h x y $.
    elsymgrn.1 $e |- A e. V $.
    elsymgrn.2 $e |- P = { x | x : A -1-1-onto-> A } $.

    $( Two ways of saying a function is a 1-1-onto mapping of A to itself.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elsymgrn $p |- ( F e. P <-> F : A -1-1-onto-> A ) $=
      ( wcel cvv wf1o elisset wf f1of fex mpan2 syl cv f1oeq1 elab2g pm5.21nii
      ) DCGDHGZBBDIZDCJUABBDKZTBBDLUBBHGTEBBHDMNOBBAPZIUAADCHBBUCDQFRS $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( Two ways to express the symmetry-group operator class abstraction.
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgoprab $p |- { <. <. f , g >. , h >. | ( f : A -1-1-onto-> A /\
                      g : A -1-1-onto-> A /\ h = ( f o. g ) ) } =
      { <. <. f , g >. , h >. | ( ( f e. P /\ g e. P ) /\ h = ( f o. g ) ) } $=
      ( cv wcel wa ccom wceq copab2 wf1o w3a elsymgrn anbi12i anbi1i df-3an
      bitr4 oprabbii eqcomi ) DIZCJZEIZCJZKZFIUDUFLMZKZDEFNBBUDOZBBUFOZUIPZDEFN
      UJUMDEFUJUKULKZUIKUMUHUNUIUEUKUGULABCUDGHQABCUFGHQRSUKULUITUAUBUC $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( The value of the symmetry group function at ` A ` .  (Contributed by
       Paul Chapman, 25-Feb-2008.) $)
    symgval $p |- ( SymGrp ` A ) = { <. <. f , g >. , h >. |
                    ( ( f e. P /\ g e. P ) /\ h = ( f o. g ) ) } $=
      ( vy csymgrp cfv cv wf1o ccom wceq w3a copab2 copab wcel wa df-symgrp
      fveq1i cvv cab equid biantru abbii eqtr f1oabexg mp2an symgoprab
      oprabex2 f1oeq2 f1oeq3 bitrd 3anbi12d oprabbidv fvopab 3eqtr ) BJKBILALZU
      TDLZMZUTUTELZMZFLVAVCNZOZPZDEFQZOAIRZKBBVAMZBBVCMZVFPZDEFQZVACSVCCSTVFTDE
      FQBJVIAIDEFUAUBAIBVHVMGDEFCCVEVMBUCSZVNCUCSGGUTUTOZBBUCUCACCBBUTMZAUDVPVO
      TZAUDHVPVQAVOVPAUEUFUGUHUIUJZVRABCDEFGHUKZULUTBOZVGVLDEFVTVBVJVDVKVFVTVBB
      UTVAMVJUTBUTVAUMUTBBVAUNUOVTVDBUTVCMVKUTBUTVCUMUTBBVCUNUOUPUQURVSUS $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( The value of the group operation of the symmetry group on ` A ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgoprval $p |- ( ( F e. P /\ G e. P ) ->
                       ( F ( SymGrp ` A ) G ) = ( F o. G ) ) $=
      ( vf vg vh wcel wa cv ccom wceq copab2 co csymgrp cfv cvv coexg coeq1
      coeq2 eqid oprabval2g mpd3an3 symgval opreqi syl5eq ) DCKZECKZLDEHMZCKIMZ
      CKLJMULUMNZOLHIJPZQZDENZDEBRSZQUJUKUQTKUPUQODECCUAHIJDECCUNUQUODUMNTULDUM
      UBUMEDUCUOUDUEUFURUODEABCHIJFGUGUHUI $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( The domain and codomain of the group operation of the symmetry group on
       ` A ` .  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgf $p |- ( SymGrp ` A ) : ( P X. P ) --> P $=
      ( vf vg vh cv ccom csymgrp cfv symgval wf1o wa wcel f1oco elsymgrn
      anbi12i 3imtr4 foprab ) FGHCCFIZGIZJZCBKLABCFGHDEMBBUBNZBBUCNZOBBUDNUBCPZ
      UCCPZOUDCPBBBUBUCQUGUEUHUFABCUBDERABCUCDERSABCUDDERTUA $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( The symmetry group on ` A ` is a group (inference version).
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symggrpi $p |- ( SymGrp ` A ) e. Grp $=
      ( vf vg vh cid cres csymgrp cfv cv ccnv cvv wcel wceq wf1o cab wa equid
      biantru abbii eqtr f1oabexg mp2an symgf w3a ccom co coass symgoprval
      3adant3 opreq1d f1oco elsymgrn anbi12i 3imtr4 sylan 3impa eqtrd 3adant1
      opreq2d sylan2 3impb 3eqtr4a f1oi mpbir mpan wf f1of fcoi2 syl sylbi
      f1ocnv mpancom f1ococnv1 isgrpi ) FGHIBJZBKLZFMZNZCBOPZWCCOPDDAMZWDQZBBOO
      ACCBBWDRZASWFWETZASEWFWGAWEWFAUAUBUCUDUEUFABCDEUGWACPZGMZCPZHMZCPZUHZWAWI
      UIZWKUIZWAWIWKUIZUIZWAWIVTUJZWKVTUJZWAWIWKVTUJZVTUJZWAWIWKUKWMWSWNWKVTUJZ
      WOWMWRWNWKVTWHWJWRWNQWLABCWAWIDEULUMUNWHWJWLXBWOQZWNCPZWLXCWHWJTZABCWNWKD
      EULBBWARZBBWIRZTBBWNRXEXDBBBWAWIUOWHXFWJXGABCWADEUPZABCWIDEUPZUQABCWNDEUP
      URUSUTVAWMXAWAWPVTUJZWQWMWTWPWAVTWJWLWTWPQWHABCWIWKDEULVBVCWHWJWLXJWQQZWH
      WPCPZXKWJWLTZABCWAWPDEULXGBBWKRZTBBWPRXMXLBBBWIWKUOWJXGWLXNXIABCWKDEUPUQA
      BCWPDEUPURVDVEVAVFVSCPZBBVSRBVGABCVSDEUPVHZWHVSWAVTUJZVSWAUIZWAXOWHXQXRQX
      PABCVSWADEULVIWHXFXRWAQZXHXFBBWAVJXSBBWAVKBBWAVLVMVNVAXFBBWBRWHWBCPZBBWAV
      OXHABCWBDEUPURZWHWBWAVTUJZWBWAUIZVSXTWHYBYCQYAABCWBWADEULVPWHXFYCVSQXHBBW
      AVQVNVAVR $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)

    $( The value of the identity element of the symmetry group on ` A `
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    symgidi $p |- ( Id ` ( SymGrp ` A ) ) = ( I |` A ) $=
      ( cid cres csymgrp cfv cgi wceq co ccom wcel wf1o f1oi elsymgrn mpbir
      symgoprval mp2an wf f1of ax-mp fcoi2 eqtr cgr wb symggrpi symgf grprnOLD
      eqid grpid eqcomi ) FBGZBHIZJIZUNUPKZUNUNUOLZUNKZURUNUNMZUNUNCNZVAURUTKVA
      BBUNOZBPZABCUNDEQRZVDABCUNUNDESTBBUNUAZUTUNKVBVEVCBBUNUBUCBBUNUDUCUEUOUFN
      VAUQUSUGABCDEUHZVDUNUPUOCUOCVFABCDEUIUJUPUKULTRUM $.
      $( [3-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A x $.
    $( The symmetry group on ` A ` is a group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    symggrp $p |- ( A e. B -> ( SymGrp ` A ) e. Grp ) $=
      ( vx wcel cvv csymgrp cfv cgr elisset c0 cif wceq fveq2 eleq1d cv wf1o
      cab 0ex elimel eqid symggrpi dedth syl ) ABDAEDZAFGZHDZABIUDUFUDAJKZFGZHD
      AJAUGLUEUHHAUGFMNCUGUGUGCOPCQZAJERSUITUAUBUC $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A a b f g h $.  $d F f w x y z $.  $d G a b c f g h w x y z $.
    $d H w x y z $.  $d P c g h $.  $d S y $.  $d U a b y $.
    $d X a b c f g h w x y z $.  $d Y y $.

    cayleylem1.1 $e |- G e. Grp $.
    cayleylem1.2 $e |- P = { f | f : X -1-1-onto-> X } $.
    cayleylem1.3 $e |- X = ran G $.
    cayleylem1.4 $e |- U = ( Id ` G ) $.
    cayleylem1.5 $e |- H = ( SymGrp ` X ) $.
    cayleylem1.6 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                             ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleylem1.7 $e |- Y = ran F $.
    cayleylem1.8 $e |- S = ( H |` ( Y X. Y ) ) $.

    $( Lemma for ~ cayleyi . $)
    cayleylem1 $p |- ( A e. X -> ( F ` A ) e. P ) $=
      ( wcel cfv wf1o cgr grplactf1o mpan crn cvv rnexg ax-mp eqeltr elsymgrn
      sylibr ) AKUCZKKAHUDZUEZUQBUCIUFUCZUPUROAFGHIKMNTQUGUHEKBUQKIUIZUJQUSUTUJ
      UCOIUFUKULUMPUNUO $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem2 $p |- F e. ( G GrpHom H ) $=
      ( vx vy vc vz vw cghom co wcel wf cv cfv wceq wral cgr wa wb csymgrp crn
      cvv rnexg ax-mp eqeltr symggrpi symgf grprnOLD rneqi eqtr4 elghom mp2an
      copab grplactfval mpan cayleylem1 eqeltrrd rgen opreq1 eqeq2d anbi2d
      opabbidv eleq1d cbvralv fopab2 bitr mpbi ccom symgoprval syl2an opreqi
      syl5eq w3a grpass grplactval mp3an1 grpcl sylan 3impa wfun wfn wi fvco2
      3expia wf1o elsymgrn sylib f1of ffn 3syl fnfun syl 3impia fveq2d 3adant1
      sylan2 3impb 3eqtrd 3eqtr4rd r19.21aiv eqid eqfnfv wss fnco frn syl3an
      3anidm23 sylanc biimprd mpani mpd eqtrd rgen2a fveq2 opreq1d eqeq12d
      opreq2d opreq2 cbvral2v mpbir2an ) GHIUGUHUIZJAGUJZUBUKZGULZUCUKZGULZIUHZ
      UUAUUCHUHZGULZUMZUCJUNUBJUNZHUOUIZIUOUIYSYTUUIUPUQNIJURULZUORDJAJHUSZUTPU
      UJUULUTUINHUOVAVBVCZOVDZVCUBUCGHIAJPAUUKUSIUSUUKAUUNDJAUUMOVEVFIUUKRVGVHV
      IVJLUKZJUIZMUKZUDUKZUUOHUHZUMZUPZLMVKZAUIZUDJUNZYTUVCUDJUURJUIZUURGULZUVB
      AUUJUVEUVFUVBUMNUUREFGHJLMSPVLVMUURABCDEFGHIJKLMNOPQRSTUAVNVOVPUVDUUPUUQE
      UKZUUOHUHZUMZUPZLMVKZAUIZEJUNYTUVCUVLUDEJUURUVGUMZUVBUVKAUVMUVAUVJLMUVMUU
      TUVIUUPUVMUUSUVHUUQUURUVGUUOHVQVRVSVTWAWBEFJAUVKGSWCWDWEUEUKZGULZUFUKZGUL
      ZIUHZUVNUVPHUHZGULZUMZUFJUNUEJUNUUIUWAUEUFJUVNJUIZUVPJUIZUPZUVRUVOUVQWFZU
      VTUWDUVOUVQUUKUHZUWEUVRUVOAUIZUVQAUIZUWFUWEUMUWBUWCDJAUVOUVQUUMOWGUVNABCD
      EFGHIJKLMNOPQRSTUAVNZUVPABCDEFGHIJKLMNOPQRSTUAVNZWHIUUKUVOUVQRWIWJUWDUUAU
      WEULZUUAUVTULZUMZUBJUNZUWEUVTUMZUWDUWMUBJUWBUWCUUAJUIZUWMUWBUWCUWPWKZUVSU
      UAHUHZUVNUVPUUAHUHZHUHZUWLUWKUUJUWQUWRUWTUMNUVNUVPUUAHJPWLVMUWBUWCUWPUWLU
      WRUMZUVSJUIZUWPUXAUWDUUJUXBUWPUXANUVSUUAEFGHJLMSPWMWNUUJUWBUWCUXBNUVNUVPH
      JPWOWNZWPWQUWQUWKUUAUVQULZUVOULZUWSUVOULZUWTUWBUWCUWPUWKUXEUMZUVOWRZUVQJW
      SZUWPUXGWTUWBUWCUXHUXIUWPUXGJUUAUVOUVQXAXBUWBUVOJWSZUXHUWBJJUVOXCZJJUVOUJ
      UXJUWBUWGUXKUWIDJAUVOUUMOXDXEJJUVOXFJJUVOXGXHZJUVOXIXJUWCJJUVQXCZJJUVQUJZ
      UXIUWCUWHUXMUWJDJAUVQUUMOXDXEZJJUVQXFZJJUVQXGXHZWHXKUWCUWPUXEUXFUMUWBUWCU
      WPUPZUXDUWSUVOUUJUWCUWPUXDUWSUMNUVPUUAEFGHJLMSPWMWNXLXMUWBUWCUWPUXFUWTUMZ
      UWBUWSJUIZUXSUXRUUJUWBUXTUXSNUVNUWSEFGHJLMSPWMWNUUJUWCUWPUXTNUVPUUAHJPWOW
      NXNXOXPXQXBXRUWDJJUMZUWNUWOJXSUWDUWOUYAUWNUPZUWEJWSZUVTJWSZUWOUYBUQUWDUBJ
      JUWEUVTXTUWBUWCUYCUXJUXIUVQUSJYAZUYCUWBUWCUWCJJUVOUVQYBUXLUXQUWCUXMUXNUYE
      UXOUXPJJUVQYCXHYDYEUWDUXBUYDUXCUXBJJUVTXCZJJUVTUJUYDUXBUVTAUIUYFUVSABCDEF
      GHIJKLMNOPQRSTUAVNDJAUVTUUMOXDXEJJUVTXFJJUVTXGXHXJYFYGYHYIYJYKUWAUUHUUBUV
      QIUHZUUAUVPHUHZGULZUMUEUFUBUCJJUVNUUAUMZUVRUYGUVTUYIUYJUVOUUBUVQIUVNUUAGY
      LYMUYJUVSUYHGUVNUUAUVPHVQXLYNUVPUUCUMZUYGUUEUYIUUGUYKUVQUUDUUBIUVPUUCGYLY
      OUYKUYHUUFGUVPUUCUUAHYPXLYNYQWEYR $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem3 $p |- ( S e. ( SubGrp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vy csubg cfv wcel cgiso co csymgrp cgr crn cvv rnexg ax-mp eqeltr
      symggrpi cayleylem2 ghomgrpi cghom wf1o wa wb wss w3a issubg mpbi
      3simp2i elgiso mp2an ghomgsg mp3an cv cgi wceq wi wral cxp cres wfn wfo
      wf eqid ghomfo forn syl eqtr grpfo fof ffn 3syl fnresdm eqcomi ghomf1o
      grpidcl grplactval mp3an13 grprid mpan eqtrd eqeq1d cid subgid fveq2i
      symgidi eqtr2 eqeq2i fveq1 fvresi syl6eq sylbir syl5bi mprgbir f1oeq3
      f1oeq2 bitr mpbir2an pm3.2i ) BIUCUDUEZGHBUFUGUEZBGHIKNIJUHUDZUIRDJAJHUJZ
      UKPHUIUEZXTUKUENHUIULUMUNZOUOUNZABCDEFGHIJKLMNOPQRSTUAUPZTUAUQZXRGHBURUGU
      EZXTBUJZGUSZYABUIUEZXRYFYHUTVANIUIUEZYIBIVBZXQYJYIYKVCYEIBVDVEVFZGHBVGVHY
      AYJGHIURUGUEZYFNYCYDBGHIKTUAVIVJZJKGUSZYHYOUBVKZGUDZBVLUDZVMZYPCVMZVNZUBJ
      YAYIYFYOUUAUBJVOVANYLYNUBBYRCGHBJKKPTBKKVPZVQZBYIUUCBVMZYLYIBUUBVRZUUDYIU
      UBKBVSUUBKBVTUUEBKKGUJZYGTYAYJYMUUFYGVMZNYCYDYAYJYMVCJYGGVSUUGBGHIJKYGPTU
      AYGWAWBJYGGWCWDVJWEZWFUUBKBWGUUBKBWHWIUUBBWJWDUMWKUUHQYRWAZWLVJYPJUEZCYQU
      DZCVMZYTYSUUJUUKYPCUUJUUKYPCHUGZYPYAUUJCJUEZUUKUUMVMNYAUUNNCHJPQWMUMZYPCE
      FGHJLMSPWNWOYAUUJUUMYPVMNYPCHJPQWPWQWRWSYSYQWTJVQZVMZUULUUPYRYQYRIVLUDZUU
      PXQYRUURVMYEYRUURIBUURWAUUIXAUMUURXSVLUDUUPIXSVLRXBDJAYBOXCWEXDXEUUQUUKCU
      UPUDZCCYQUUPXFUUNUUSCVMUUOJCXGUMXHXIXJXKYOJYGGUSZYHKYGVMYOUUTVAUUHKYGJGXL
      UMJXTVMUUTYHVAPJXTYGGXMUMXNVEXOXP $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F f $.  $d G a b f g h $.  $d X a b f g h $.
    cayleyi.1 $e |- G e. Grp $.
    cayleyi.2 $e |- X = ran G $.
    cayleyi.3 $e |- H = ( SymGrp ` X ) $.
    cayleyi.4 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                          ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleyi.5 $e |- Y = ran F $.
    cayleyi.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is
       an isomorphism between ` G ` and the subgroup ` S ` of the symmetry
       group ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by
       Paul Chapman, 3-Mar-2008.) $)
    cayleyi $p |- ( S e. ( SubGrp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vf cv wf1o cab cgi cfv eqid cayleylem3 ) GGQRSQTZAEUAUBZQBCDEFGHIJKUEUC
      LUFUCMNOPUD $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F f h $.  $d G a b g j $.  $d G f h $.  $d H f h $.  $d Q a b g j $.
    $d Q f h $.  $d S h $.  $d X a b g j $.
    cayleythlem.1 $e |- Q = { <. <. x , x >. , x >. } $.
    cayleythlem.2 $e |- X = ran if ( G e. Grp , G , Q ) $.
    cayleythlem.3 $e |- H = ( SymGrp ` X ) $.
    cayleythlem.4 $e |- F = { <. g , j >. | ( g e. X /\ j = { <. a , b >. |
      ( a e. X /\ b = ( g if ( G e. Grp , G , Q ) a ) ) } ) } $.
    cayleythlem.5 $e |- Y = ran F $.
    cayleythlem.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Lemma for ~ cayleyth . $)
    cayleythlem $p |- ( G e. Grp ->
        E. f E. h e. ( SubGrp ` ( SymGrp ` ran G ) ) f e. ( G GrpIso h ) ) $=
      ( cgr wcel cv cgiso co csubg cfv wrex wex crn csymgrp cif wceq opreq1
      eleq2d rexbidv exbidv wa cop csn visset grpsn eqeltr elimel cayleyi
      opreq2 rcla4ev ax-mp copab cvv rnexg fopabex2 eleq1 cla4ev dedth iftrue
      rneqd syl5eq fveq2d rexeq1d mpbid ) IUAUBZDUCZIFUCZUDUEZUBZFJUFUGZUHZDUIZ
      WFFIUJZUKUGZUFUGZUHZDUIWBWIWCWBIBULZWDUDUEZUBZFWGUHZDUIZIBIWNUMZWHWQDWSWF
      WPFWGWSWEWOWCIWNWDUDUNUOUPUQHWOUBZFWGUHZWRCWGUBHWNCUDUEZUBZURXACEGHWNJKLM
      NIBUABAUCZXDUSXDUSUTUAOXDAVAVBVCVDZPQRSTVEWTXCFCWGWDCUMWOXBHWDCWNUDVFUOVG
      VHWQXADHEGKMUCZKUBNUCEUCXFWNUEUMURMNVIHKWNUJZVJPWNUAUBXGVJUBXEWNUAVKVHVCR
      VLWCHUMWPWTFWGWCHWOVMUPVNVHVOWBWHWMDWBWFFWGWLWBJWKUFWBKUKUGWKJWBKWJUKWBXG
      WJKWBWNIWBIBVPVQPVRVSQVRVSVTUQWA $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d G a b f g h j $.  $d a b f g h j x $.
    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic
       to a subgroup of the symmetry group on the underlying set of ` G ` .
       (For any group ` G ` there exists an isomorphism ` f ` between ` G ` and
       a subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. Grp -> E. f E. h e. ( SubGrp ` ( SymGrp ` ran G ) )
                                      f e. ( G GrpIso h ) ) $=
      ( vx vg vj va vb cv cop csn cgr wcel cif crn csymgrp cfv co wceq wa
      copab cxp cres eqid cayleythlem ) DDIZUFJUFJKZCLMCUGNZOZPQZEIZUIMFIGIZUIM
      HIUKULUHRSTGHUASTEFUAZOZUNUBUCZAEBFUMCUJUIUNGHUGUDUIUDUJUDUMUDUNUDUOUDUE
      $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

$( (End of Paul Chapmans's sandbox.) $)


