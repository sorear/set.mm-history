$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                The Kuratowski closure-complement theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Get an upper bound on a concretely specified finite set.  Base case:
     singleton set. $)
  hashsnlei $p |- ( { A } e. Fin /\ ( # ` { A } ) <_ 1 ) $=
    ( csn cfn wcel chash cfv c1 cle wbr snfi cvv hashsng 1re leidi syl6eqbr cc0
    wn c0 wceq snprc biimpi fveq2d hash0 syl6eq 0re lt01 ltleii pm2.61i pm3.2i
    ) ABZCDUJEFZGHIZAJAKDZULUMUKGGHAKLGMNOUMQZUKPGHUNUKREFPUNUJREUNUJRSATUAUBUC
    UDPGUEMUFUGOUHUI $.
    $( [11-Feb-2015] $)

  ${
    hashunlei.c $e |- C = ( A u. B ) $.
    hashunlei.a $e |- ( A e. Fin /\ ( # ` A ) <_ K ) $.
    hashunlei.b $e |- ( B e. Fin /\ ( # ` B ) <_ M ) $.
    hashunlei.k $e |- K e. NN0 $.
    hashunlei.m $e |- M e. NN0 $.
    hashunlei.n $e |- N = ( K + M ) $.
    $( Get an upper bound on a concretely specified finite set.  Induction
       step: union of two finite bounded sets. $)
    hashunlei $p |- ( C e. Fin /\ ( # ` C ) <_ N ) $=
      ( cfn wcel chash cfv cle wbr mp2an nn0rei cun simpli eqeltri caddc fveq2i
      unfi co hashun2 eqbrtri simpri cn0 hashcl ax-mp le2addi breqtrri readdcli
      cr letri pm3.2i ) CMNZCOPZFQRZCABUAZMGAMNZBMNZVCMNVDAOPZDQRZHUBZVEBOPZEQR
      ZIUBZABUFSUCZVAVFVIUDUGZQRVMFQRVBVAVCOPZVMQCVCOGUEVDVEVNVMQRVHVKABUHSUIVM
      DEUDUGZFQVGVJVMVOQRVDVGHUJVEVJIUJVFVIDEVFVDVFUKNVHAULUMTZVIVEVIUKNVKBULUM
      TZDJTZEKTZUNSLUOVAVMFVAUTVAUKNVLCULUMTVFVIVPVQUPFVOUQLDEVRVSUPUCURSUS $.
      $( [11-Feb-2015] $)
  $}

  ${
    hashsslei.b $e |- B C_ A $.
    hashsslei.a $e |- ( A e. Fin /\ ( # ` A ) <_ N ) $.
    hashsslei.n $e |- N e. NN0 $.
    $( Get an upper bound on a concretely specified finite set.  Transfer
       boundedness to a subset. $)
    hashsslei $p |- ( B e. Fin /\ ( # ` B ) <_ N ) $=
      ( cfn wcel chash cfv cle wbr wss simpli ssfi mp2an hashcl ax-mp nn0rei
      cn0 cdom ssdomg mp2 wb hashdom mpbir simpri letri pm3.2i ) BGHZBIJZCKLZAG
      HZBAMZUJUMAIJZCKLZENZDABOPZUKUOKLZUPULUSBAUALZUJUNUTURDBAGUBUCUJUMUSUTUDU
      RUQBAUEPUFUMUPEUGUKUOCUKUJUKTHURBQRSUOUMUOTHUQAQRSCFSUHPUI $.
      $( [11-Feb-2015] $)
  $}

  $( An unordered pair has at most two elements. $)
  hashprlei $p |- ( { A , B } e. Fin /\ ( # ` { A , B } ) <_ 2 ) $=
    ( csn cpr c1 c2 df-pr hashsnlei 1nn0 df-2 hashunlei ) ACBCABDEEFABGAHBHIIJK
    $.
    $( [11-Feb-2015] $)

  $( An unordered triple has at most three elements. $)
  hashtplei $p |- ( { A , B , C } e. Fin /\ ( # ` { A , B , C } ) <_ 3 ) $=
    ( cpr csn ctp c2 c1 c3 df-tp hashprlei hashsnlei 2nn0 1nn0 df-3 hashunlei )
    ABDCEABCFGHIABCJABKCLMNOP $.
    $( [11-Feb-2015] $)

  ${
    $d s x A $.  $d s x K $.  $d s x y T $.  $d s x y X $.
    kur14lem.j $e |- J e. Top $.
    kur14lem.x $e |- X = U. J $.
    ${
      kur14lem1.a $e |- A C_ X $.
      kur14lem1.c $e |- ( X \ A ) e. T $.
      kur14lem1.k $e |- ( K ` A ) e. T $.
      $( Lemma for ~ kur14 . $)
      kur14lem1 $p |- ( N = A ->
        ( N C_ X /\ { ( X \ N ) , ( K ` N ) } C_ T ) ) $=
        ( wceq wss cdif cfv cpr wcel syl6eqel jca ax-mp sseq1 mpbiri difeq2 cvv
        wa fveq2 ctop topopn difexg fvex prss sylib ) EALZEFMZFENZEDOZPBMZUMUNA
        FMIEAFUAUBUMUOBQZUPBQZUEUQUMURUSUMUOFANBEAFUCJRUMUPADOBEADUFKRSUOUPBFCQ
        ZUOUDQCUGQUTGCFHUHTFECUITEDUJUKULS $.
        $( [11-Feb-2015] $)
    $}

    kur14lem.k $e |- K = ( cls ` J ) $.
    kur14lem.i $e |- I = ( int ` J ) $.
    kur14lem.a $e |- A C_ X $.
    $( Lemma for ~ kur14 .  Write interior in terms of closure and complement:
       ` i A = c k c A ` where ` c ` is complement and ` k ` is closure. $)
    kur14lem2 $p |- ( I ` A ) = ( X \ ( K ` ( X \ A ) ) ) $=
      ( cnt cfv cdif ccl ctop wcel wss wceq ntrval2 fveq1i difeq2i 3eqtr4i
      mp2an ) ACKLZLZEEAMZCNLZLZMZABLEUFDLZMCOPAEQUEUIRFJACEGSUCABUDITUJUHEUFDU
      GHTUAUB $.
      $( [11-Feb-2015] $)

    $( Lemma for ~ kur14 .  A closure is a subset of the base set. $)
    kur14lem3 $p |- ( K ` A ) C_ X $=
      ( cfv ccl fveq1i ctop wcel wss clsss3 mp2an eqsstri ) ADKACLKZKZEADTHMCNO
      AEPUAEPFJACEGQRS $.
      $( [11-Feb-2015] $)

    $( Lemma for ~ kur14 .  Complementation is an involution on the set of
       subsets of a topology. $)
    kur14lem4 $p |- ( X \ ( X \ A ) ) = A $=
      ( wss cdif wceq dfss4 mpbi ) AEKEEALLAMJAENO $.
      $( [11-Feb-2015] $)

    $( Lemma for ~ kur14 .  Closure is an idempotent operation in the set of
       subsets of a topology. $)
    kur14lem5 $p |- ( K ` ( K ` A ) ) = ( K ` A ) $=
      ( ccl cfv ctop wcel wss wceq clsidm mp2an fveq1i fveq12i 3eqtr4i ) ACKLZL
      ZUBLZUCADLZDLUECMNAEOUDUCPFJACEGQRUEUCDUBHADUBHSZTUFUA $.
      $( [11-Feb-2015] $)

    kur14lem.b $e |- B = ( X \ ( K ` A ) ) $.
    $( Lemma for ~ kur14 .  If ` k ` is the complementation operator and ` k `
       is the closure operator, this expresses the identity
       ` k c k A = k c k c k c k A ` for any subset ` A ` of the topological
       space.  This is the key result that lets us cut down long enough
       sequences of ` c k c k ... ` that arise when applying closure and
       complement repeatedly to ` A ` , and explains why we end up with a
       number as large as ` 1 4 ` , yet no larger. $)
    kur14lem6 $p |- ( K ` ( I ` ( K ` B ) ) ) = ( K ` B ) $=
      ( cfv wss cdif eqsstri fveq1i mp2an clsss 3sstr4i ccl ctop wcel kur14lem3
      difss cnt ntrss2 kur14lem5 sseqtri kur14lem2 clsss3 fveq2i difeq2i eqtr4i
      mp3an eqtr3i sscon ax-mp eqssi ) BEMZCMZEMZUTVBUTEMZUTVADUAMZMZUTVDMZVBVC
      DUBUCZUTFNZVAUTNVEVFNGBCDEFGHIJBFAEMZOZFLFVIUEPZUDZVAUTDUFMZMZUTUTCVMJQVG
      VHVNUTNGVLUTDFHUGRPUTVADFHSUOVAEVDIQZUTEVDIQTBCDEFGHIJVKUHUIBVDMZVEUTVBVG
      VAFNBVANVPVENGVAFFUTOZEMZOZFUTCDEFGHIJVLUJZFVRUEPVJVSBVAVRVINVJVSNVQVDMZV
      IVDMZVRVIVGVIFNZVQVINWAWBNGVIAVDMZFAEVDIQVGAFNWDFNGKADFHUKRPZVQVIVMMZVIVI
      CMZVQWFWGFVJEMZOVQVICDEFGHIJWEUJUTWHFBVJELULUMUNVICVMJQUPVGWCWFVINGWEVIDF
      HUGRPVIVQDFHSUOVQEVDIQVIEMVIWBACDEFGHIJKUHVIEVDIQUPTVRVIFUQURLVTTVABDFHSU
      OBEVDIQVOTUS $.
      $( [11-Feb-2015] $)

    kur14lem.c $e |- C = ( K ` ( X \ A ) ) $.
    kur14lem.d $e |- D = ( I ` ( K ` A ) ) $.
    kur14lem.t $e |- T = ( ( ( { A , ( X \ A ) , ( K ` A ) } u.
      { B , C , ( I ` A ) } ) u. { ( K ` B ) , D , ( K ` ( I ` A ) ) } ) u.
      ( { ( I ` C ) , ( K ` D ) , ( I ` ( K ` B ) ) } u.
      { ( K ` ( I ` C ) ) , ( I ` ( K ` ( I ` A ) ) ) } ) ) $.
    $( Lemma for ~ kur14 : main proof.  The set ` T ` here contains all the
       distinct combinations of ` k ` and ` c ` that can arise, and we prove
       here that applying ` k ` or ` c ` to any element of ` T ` yields another
       elemnt of ` T ` .  In operator shorthand, we have
       ` T = { A , c A , k A `
       ` , c k A , k c A , c k c A , k c k A , c k c k A , k c k c A , `
       ` c k c k c A , k c k c k A , c k c k c k A , k c k c k c A , `
       ` c k c k c k c A } ` .  From the identities ` c c A = A ` and
       ` k k A = k A ` , we can reduce any operator combination containing two
       adjacent identical operators, which is why the list only contains
       alternating sequences.  The reason the sequences don't keep going after
       a certain point is due to the identity ` k c k A = k c k c k c k A ` ,
       proved in ~ kur14lem6 . $)
    kur14lem7 $p |- ( N e. T ->
      ( N C_ X /\ { ( X \ N ) , ( K ` N ) } C_ T ) ) $=
      ( cdif wss cfv cpr wa ctp cun wcel wo elun w3o eltpi ssun1 sseqtr4i sstri
      wceq ctop topopn ax-mp elexi difss ssexi tpid2 sselii kur14lem3 kur14lem1
      tpid3 kur14lem4 tpid1 eqeltri ssun2 eqsstri kur14lem5 3jaoi difeq2i eqtri
      eqeltrri syl kur14lem2 eqtr4i fveq2i 3eqtr4i eqtr3i sylbi prid1 kur14lem6
      jaoi elpri prid2 eleq2s ) IJUAJITIHUBUCEUAUDZIAJATZAHUBZUEZBCAFUBZUEZUFZB
      HUBZDWNHUBZUEZUFZCFUBZDHUBZWQFUBZUEZXAHUBZWRFUBZUCZUFZUFZEIXIUGIWTUGZIXHU
      GZUHWJIWTXHUIXJWJXKXJIWPUGZIWSUGZUHWJIWPWSUIXLWJXMXLIWMUGZIWOUGZUHWJIWMWO
      UIXNWJXOXNIAUOZIWKUOZIWLUOZUJWJIAWKWLUKXPWJXQXRAEGHIJKLOWMEWKWMWPEWMWOULW
      PWTEWPWSULWTXIEWTXHULSUMZUNZUNZAWKWLWKJJGGUPUGJGUGKGJLUQURUSZJAUTZVAVBVCW
      MEWLYAAWKWLWLJYBAFGHJKLMNOVDZVAVFVCZVEWKEGHIJKLYCJWKTAEAFGHJKLMNOVGWMEAYA
      AWKWLAJYBOVAVHVCVICWKHUBZEQWOECWOWPEWOWMVJXTUNZBCWNCJYBCYFJQWKFGHJKLMNYCV
      DVKZVAVBVCZVPVEWLEGHIJKLYDBJWLTZEPWOEBYGBCWNBJYBBYJJPJWLUTVKZVAVHVCVPWLHU
      BWLEAFGHJKLMNOVLYEVIVEVMVQXOIBUOZICUOZIWNUOZUJWJIBCWNUKYLWJYMYNBEGHIJKLYK
      JBTZWLEYOJYJTWLBYJJPVNWLFGHJKLMNYDVGVOYEVIWSEWQWSWTEWSWPVJXSUNZWQDWRWQJYB
      BFGHJKLMNYKVDZVAVHVCZVECEGHIJKLYHJCTZWNEYSJYFTZWNCYFJQVNAFGHJKLMNOVRZVSZW
      OEWNYGBCWNWNJYBWNYTJUUAJYFUTVKZVAVFVCVICHUBZCEYFHUBYFUUDCWKFGHJKLMNYCVLCY
      FHQVTQWAYIVIVEWNEGHIJKLUUCJWNTZCEJYSTUUECYSWNJUUBVNCFGHJKLMNYHVGWBYIVIWSE
      WRYPWQDWRWRJYBWNFGHJKLMNUUCVDZVAVFVCZVEVMVQWFWCXMIWQUOZIDUOZIWRUOZUJWJIWQ
      DWRUKUUHWJUUIUUJWQEGHIJKLYQDJWQTZEWLFUBZJYJHUBZTZDUUKWLFGHJKLMNYDVRZRWQUU
      MJBYJHPVTVNWAZWSEDYPWQDWRDJYBDUUNJDUULUUNRUUOVOJUUMUTVKZVAVBVCVPWQHUBWQEB
      FGHJKLMNYKVLYRVIVEDEGHIJKLUUQJDTZWQEUURJUUKTWQDUUKJUUPVNWQFGHJKLMNYQVGVOY
      RVIXDEXBXDXHEXDXGULXHXIEXHWTVJSUMZUNZXAXBXCXBJYBDFGHJKLMNUUQVDZVAVBVCZVEW
      REGHIJKLUUFXAJWRTZEXAJYSHUBZTZUVCCFGHJKLMNYHVRZUVDWRJYSWNHUUBVTVNVOZXDEXA
      UUTXAXBXCXAJYBXAUVEJUVFJUVDUTVKZVAVHVCVPWRHUBWREWNFGHJKLMNUUCVLUUGVIVEVMV
      QWFWCXKIXDUGZIXGUGZUHWJIXDXGUIUVIWJUVJUVIIXAUOZIXBUOZIXCUOZUJWJIXAXBXCUKU
      VKWJUVLUVMXAEGHIJKLUVHJXATZWREUVNJUVCTWRXAUVCJUVGVNWRFGHJKLMNUUFVGVOUUGVI
      XGEXEXGXHEXGXDVJUUSUNZXEXFXEJYBXAFGHJKLMNUVHVDZVAWDVCZVEXBEGHIJKLUVAJXBTZ
      XCEUVRJUUKHUBZTZXCXBUVSJDUUKHUUPVTVNWQFGHJKLMNYQVRZVSZXDEXCUUTXAXBXCXCJYB
      XCUVTJUWAJUVSUTVKZVAVFVCVIXBHUBXBEDFGHJKLMNUUQVLUVBVIVEXCEGHIJKLUWCJXCTZX
      BEJUVRTUWDXBUVRXCJUWBVNXBFGHJKLMNUVAVGWBUVBVIXCHUBWQEABFGHJKLMNOPWEYRVIVE
      VMVQUVJIXEUOZIXFUOZUHWJIXEXFWGUWEWJUWFXEEGHIJKLUVPJXETZXFEUWGJUVCHUBZTZXF
      XEUWHJXAUVCHUVGVTVNWRFGHJKLMNUUFVRZVSZXGEXFUVOXEXFXFJYBXFUWIJUWJJUWHUTVKZ
      VAWHVCVIXEHUBXEEXAFGHJKLMNUVHVLUVQVIVEXFEGHIJKLUWLJXFTZXEEJUWGTUWMXEUWGXF
      JUWKVNXEFGHJKLMNUVPVGWBUVQVIXFHUBWREWKWNFGHJKLMNYCUUAWEUUGVIVEWFVQWFWCWFW
      CSWI $.
      $( [11-Feb-2015] $)

    $( Lemma for ~ kur14 .  Show that the set ` T ` contains at most ` 1 4 `
       elements.  (It could be less if some of the operators take the same
       value for a given set, but Kuratowski showed that this upper bound of
       ` 1 4 ` is tight in the sense that there exist topological spaces and
       subsets of these spaces for which all ` 1 4 ` generated sets are
       distinct, and indeed the real numbers form such a topological space.) $)
    kur14lem8 $p |- ( T e. Fin /\ ( # ` T ) <_ ( 10 + 4 ) ) $=
      ( c9 caddc cdif cfv ctp cun cpr c5 c10 c4 co eqid hashtplei 3nn0 cn0 wcel
      c6 c3 idi 3p3e6 eqcomi hashunlei 6nn nnnn0i 6p3e9 c2 hashprlei 2nn0 3p2e5
      9nn 5nn c1 nncni ax-1cn 4nn add32i addassi eqtri df-10 oveq1i df-5 oveq2i
      3eqtr4i ) AIAUAZAHUBZUCZBCAFUBZUCZUDZBHUBZDWEHUBZUCZUDZCFUBZDHUBZWHFUBZUC
      ZWLHUBZWIFUBZUEZUDZESUFUGUHTUIZRWGWJWKUOUPSWKUJWDWFWGUPUPUOWGUJAWBWCUKBCW
      EUKULUPUMUNULUQZUPUPTUIUOURUSUTWHDWIUKUOVAVBXAUOUPTUISVCUSUTWOWRWSUPVDUFW
      SUJWLWMWNUKWPWQVEXAVFUPVDTUIUFVGUSUTSVHVBUFVIVBSVJTUIZUHTUIZSUHVJTUIZTUIZ
      WTSUFTUIXCSUHTUIVJTUIXESVJUHSVHVKZVLUHVMVKZVNSUHVJXFXGVLVOVPUGXBUHTVQVRUF
      XDSTVSVTWAUT $.
      $( [11-Feb-2015] $)

    kur14lem.s $e |- S = |^| { x e. ~P ~P X |
      ( A e. x /\ A. y e. x { ( X \ y ) , ( K ` y ) } C_ x ) } $.
    $( Lemma for ~ kur14 .  Since the set ` T ` is closed under closure and
       complement, it contains the minimal set ` S ` as a subset, so ` S ` also
       has at most ` 1 4 ` elements.  (Indeed ` S = T ` , and it's not hard to
       prove this, but we don't need it for this proof.) $)
    kur14lem9 $p |- ( S e. Fin /\ ( # ` S ) <_ ( 10 + 4 ) ) $=
      ( vs c10 c4 caddc co cv wcel cdif cfv cpr wss wral cpw crab cint elintrab
      wa vex ctp cun ssun1 sseqtr4i sstri topopn ax-mp elexi ssexi tpid1 sselii
      wi ctop kur14lem7 simprd rgen pm3.2i simpld elpw2 sylibr ssriv pwex mpbir
      wceq eleq2 sseq2 raleqbi1dv anbi12d imbi12d rcla4cv mpi eqsstri kur14lem8
      sylbi 10nn nnnn0i 4nn0 nn0addcli hashsslei ) HGUDUEUFUGGCAUHZUIZLBUHZUJXB
      KUKULZWTUMZBWTUNZUSZALUOZUOZUPUQZHUBUCXIHUCUHZXIUIXFXJWTUIZVLZAXHUNZXJHUI
      ZXFAXJXHUCUTURXMCHUIZXCHUMZBHUNZUSZXNXOXQCLCUJZCKUKZVAZHCYAYADECIUKZVAZVB
      ZHYAYCVCYDYDDKUKZFYBKUKZVAZVBZHYDYGVCYHYHEIUKZFKUKYEIUKVAYIKUKYFIUKULVBZV
      BHYHYJVCUAVDVEVECXSXTCLLJJVMUILJUIMJLNVFVGVHZQVIVJVKXPBHXBHUIZXBLUMZXPCDE
      FHIJKXBLMNOPQRSTUAVNZVOVPVQXMHXHUIZXRXNVLZYOHXGUMBHXGYLYMXBXGUIYLYMXPYNVR
      XBLYKVSVTWAHXGLYKWBVSWCXLYPAHXHWTHWDZXFXRXKXNYQXAXOXEXQWTHCWEXDXPBWTHWTHX
      CWFWGWHWTHXJWEWIWJWKWKWNWAWLCDEFHIJKLMNOPQRSTUAWMUDUEUDWOWPWQWRWS $.
      $( [11-Feb-2015] $)
  $}

  ${
    $d x y A $.  $d x y J $.  $d x y K $.  $d x y X $.
    kur14lem10.j $e |- J e. Top $.
    kur14lem10.x $e |- X = U. J $.
    kur14lem10.k $e |- K = ( cls ` J ) $.
    kur14lem10.s $e |- S = |^| { x e. ~P ~P X |
      ( A e. x /\ A. y e. x { ( X \ y ) , ( K ` y ) } C_ x ) } $.
    kur14lem10.a $e |- A C_ X $.
    $( Lemma for ~ kur14 .  Discharge the set ` T ` . $)
    kur14lem10 $p |- ( S e. Fin /\ ( # ` S ) <_ ( 10 + 4 ) ) $=
      ( cfv cdif cnt ctp cun cpr eqid kur14lem9 ) ABCGCFMZNZGCNZFMZUAEOMZMZDCUC
      UAPUBUDCUEMZPQUBFMZUFUGFMZPQUDUEMZUFFMUHUEMPUJFMUIUEMRQQZUEEFGHIJUESLUBSU
      DSUFSUKSKT $.
      $( [11-Feb-2015] $)
  $}

  ${
    $d x y A $.  $d x y J $.  $d x X $.
    kur14.x $e |- X = U. J $.
    kur14.k $e |- K = ( cls ` J ) $.
    kur14.s $e |- S = |^| { x e. ~P ~P X |
      ( A e. x /\ A. y e. x { ( X \ y ) , ( K ` y ) } C_ x ) } $.
    $( Kuratowski's closure-complement theorem.  There are at most 14 sets
       which can be obtained by the application of the closure and complement
       operations to a set in a topological space.  (Contributed by Mario
       Carneiro, 11-Feb-2015.) $)
    kur14 $p |- ( ( J e. Top /\ A C_ X ) ->
      ( S e. Fin /\ ( # ` S ) <_ ( 10 + 4 ) ) ) $=
      ( wss ctop wcel cfn chash cfv cle wa c0 cpw c10 c4 caddc wbr cif cdif cpr
      co cv wral crab cint csn cuni ccl wceq eleq1 anbi1d rabbidv inteqd syl5eq
      eleq1d fveq2d breq1d anbi12d unieq pweqd sseq2d sn0top elimel ax-mp elpw2
      cvv uniexg syl6bbr ifbid difeq1d fveq2 fveq1d preq12d sseq1d ralbidv eqid
      rabeqbidv 0elpw elpwi kur14lem10 dedth2h ancoms ) CGKZELMZDNMZDOPZUAUBUCU
      HZQUDZRZWJWKWPWJCSUEZAUIZMZGBUIZUFZWTFPZUGZWRKZBWRUJZRZAGTZTZUKZULZNMZXJO
      PZWNQUDZRCWKESUMZUEZUNZTZMZCSUEZWRMZXPWTUFZWTXOUOPZPZUGZWRKZBWRUJZRZAXQTZ
      UKZULZNMZYJOPZWNQUDZRCESXNCWQUPZWLXKWOXMYNDXJNYNDCWRMZXERZAXHUKZULXJJYNYQ
      XIYNYPXFAXHYNYOWSXECWQWRUQURUSUTVAZVBYNWMXLWNQYNDXJOYRVCVDVEEXOUPZXKYKXMY
      MYSXJYJNYSXIYIYSXFYGAXHYHYSXGXQYSGXPYSGEUNXPHEXOVFVAZVGVGYSWSXTXEYFYSWQXS
      WRYSWJXRCSYSWJCXPKXRYSGXPCYTVHCXPXOLMXPVMMEXNLVIVJZXOLVNVKVLVOVPVBYSXDYEB
      WRYSXCYDWRYSXAYAXBYCYSGXPWTYTVQYSWTFYBYSFEUOPYBIEXOUOVRVAVSVTWAWBVEWDUTZV
      BYSXLYLWNQYSXJYJOUUBVCVDVEABXSYJXOYBXPUUAXPWCYBWCYJWCXSXQMXSXPKCSXQXPWEVJ
      XSXPWFVKWGWHWI $.
      $( [11-Feb-2015] $)
  $}

