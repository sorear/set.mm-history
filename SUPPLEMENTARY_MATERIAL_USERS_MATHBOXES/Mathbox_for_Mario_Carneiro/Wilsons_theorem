$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Wilson's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The only elements are equal to their own inverses in the multiplicative
     group of nonzero elements in ` ZZ / P ZZ ` are ` 1 ` and
     ` -u 1 == P - 1 ` .  (Note that from ~ prmdiveq ,
     ` ( N ^ ( P - 2 ) ) mod P ` is the modular inverse of ` N ` in
     ` ZZ / P ZZ ` . $)
  wilthlem1 $p |- ( ( P e. Prime /\ N e. ( 1 ... ( P - 1 ) ) ) ->
    ( N = ( ( N ^ ( P - 2 ) ) mod P ) <-> ( N = 1 \/ N = ( P - 1 ) ) ) ) $=
    ( wcel c1 cmin co cfz caddc cmul cdivides wbr c2 cmo wceq cz wb syl syl3anc
    cc0 cc cprime wa wo simpl elfzelz adantl peano2zm peano2zdi euclemma sseldi
    cexp zsscn mulcom syl2anc ax-1cn subsq sylancl sqval sq1 a1i oveq12d breq2d
    3eqtr2d 0p1e1 oveq1i cvv 0z ovex fzp1ss mp2an eqsstri simpr biantrurd wn cn
    wss prmnn fzm1ndvds sylan eqid prmdiveq 3bitrd 1z moddvds cr crp cle elfznn
    clt nnre nnrp nnnn0 nn0ge0 3syl elfzle2 prmz adantr zltlem1 mpbird syl22anc
    cn0 modid cuz prmuz2 eluz2b2 simprbi 1mod eqeq12d bitr3d cneg znegcl mulid2
    cfv nncn oveq2d negcli addcom sylancr negsub 3eqtrd oveq1d modcyc peano2rem
    1re renegcli nnm1nn0 ltm1 3eqtr3d subneg 3bitr3rd orbi12d 3bitr3d ) AUACZBD
    ADEFZGFZCZUBZABDEFZBDHFZIFZJKZAYRJKZAYSJKZUCZBBALEFUKFAMFZNZBDNZBYNNZUCYQYM
    YROCZYSOCUUAUUDPYMYPUDZYQBOCZUUIYPUUKYMBDYNUEUFZBUGQZYQBUULUHZAYRYSUIRYQUUA
    ABBIFZDEFZJKZBSYNGFZCZUUQUBZUUFYQYTUUPAJYQYTYSYRIFZBLUKFZDLUKFZEFZUUPYQYRTC
    YSTCYTUVANYQOTYRULUUMUJYQOTYSULUUNUJYRYSUMUNYQBTCZDTCZUVDUVANYQOTBULUULUJZU
    OBDUPUQYQUVBUUOUVCDEYQUVEUVBUUONUVGBURQUVCDNYQUSUTVAVCVBYQUUSUUQYQYOUURBYOS
    DHFZYNGFZUURDUVHYNGVDVESOCYNVFCUVIUURVPVGADEVHSYNVFVIVJVKYMYPVLUJVMYQYMUUKA
    BJKVNZUUTUUFPUUJUULYMAVOCZYPUVJAVQZABVRVSBAUUEBUUEVTWARWBYQUUBUUGUUCUUHYQBA
    MFZDAMFZNZUUBUUGYQUVKUUKDOCZUVOUUBPYQYMUVKUUJUVLQZUULUVPYQWCUTZBDAWDRYQUVMB
    UVNDYQBWECZAWFCZSBWGKZBAWIKZUVMBNYQBVOCZUVSYPUWCYMBYNWHUFZBWJQYQUVKUVTUVQAW
    KQZYQUWCBXACUWAUWDBWLBWMWNYQUWBBYNWGKZYPUWFYMBDYNWOUFYQUUKAOCZUWBUWFPUULYMU
    WGYPAWPWQBAWRUNWSBAXBWTZYQAWECZDAWIKZUVNDNYQUVKUWIUVQAWJQZYQALXCXMCZUWJYQYM
    UWLUUJAXDQUWLUVKUWJAXEXFQAXGUNXHXIYQUVMDXJZAMFZNZABUWMEFZJKZUUHUUCYQUVKUUKU
    WMOCZUWOUWQPUVQUULYQUVPUWRUVRDXKQBUWMAWDRYQUVMBUWNYNUWHYQUWMDAIFZHFZAMFZYNA
    MFZUWNYNYQUWTYNAMYQUWTUWMAHFZAUWMHFZYNYQUWSAUWMHYQATCZUWSANYQUVKUXEUVQAXNQZ
    AXLQXOYQUWMTCUXEUXCUXDNDUOXPUXFUWMAXQXRYQUXEUVFUXDYNNUXFUOADXSUQXTYAYQUWMWE
    CZUVTUVPUXAUWNNUXGYQDYDYEUTUWEUVRUWMADYBRYQYNWECZUVTSYNWGKZYNAWIKZUXBYNNYQU
    WIUXHUWKAYCQUWEYQYNXACZUXIYQUVKUXKUVQAYFQYNWMQYQUWIUXJUWKAYGQYNAXBWTYHXHYQU
    WPYSAJYQUVEUVFUWPYSNUVGUOBDYIUQVBYJYKYL $.
    $( [24-Jan-2015] $)

  ${
    $d s t x y A $.  $d k s t w x y z P $.  $d w x y z ph $.  $d s w x y z S $.
    $d s t w x y z T $.
    wilthlem.t $e |- T = ( mulGrp ` CCfld ) $.
    wilthlem.a $e |- A = { x e. ~P ( 1 ... ( P - 1 ) ) | ( ( P - 1 ) e. x /\
      A. y e. x ( ( y ^ ( P - 2 ) ) mod P ) e. x ) } $.
    ${
      wilthlem2.p $e |- ( ph -> P e. Prime ) $.
      wilthlem2.s $e |- ( ph -> S e. A ) $.
      wilthlem2.r $e |- ( ph -> A. s e. A ( s C. S ->
        ( ( T gsum ( _I |` s ) ) mod P ) = ( -u 1 mod P ) ) ) $.
      $( Lemma for ~ wilth : induction step.  The "hand proof" version of this
         theorem works by writing out the list of all numbers from ` 1 ` to
         ` P - 1 ` in pairs such that a number is paired with its inverse.
         Every number has a unique inverse different from itself except ` 1 `
         and ` P - 1 ` , and so each pair multiplies to ` 1 ` , and ` 1 ` and
         ` P - 1 == -u 1 ` multiply to ` -u 1 ` , so the full product is equal
         to ` -u 1 ` .  Here we make this precise by doing the product pair by
         pair.

         The induction hypothesis says that every subset ` S ` of
         ` 1 ... ( P - 1 ) ` that is closed under inverse (i.e. all pairs are
         matched up) and contains ` P - 1 ` multiplies to ` -u 1 mod P ` .
         Given such a set, we take out one element ` z =/= P - 1 ` .  If there
         are no such elements, then ` S = { P - 1 } ` which forms the base
         case.  Otherwise, ` S \ { z , z ^ -u 1 } ` is also closed under
         inverse and contains ` P - 1 ` , so the induction hypothesis says that
         this equals ` -u 1 ` ; and the remaining two elements are either equal
         to each other, in which case ~ wilthlem1 gives that ` z = 1 ` or
         ` P - 1 ` , and we've already excluded the second case, so the product
         gives ` 1 ` ; or ` z =/= z ^ -u 1 ` and their product is ` 1 ` .  In
         either case the accumulated product is unaffected. $)
      wilthlem2 $p |- ( ph ->
        ( ( T gsum ( _I |` S ) ) mod P ) = ( -u 1 mod P ) ) $=
        ( c1 co cmo wceq wcel cc cz vz vw cmin csn wss cid cres cgsu wa cv cmpt
        simpr cexp wral cfz eleq2 raleqbi1dv anbi12d elrab2 sylib simprd simpld
        adantr reseq2d mptresid oveq2d oveq1d cmul caddc syl ax-1cn sylancl 1re
        cn ccnfld ccrg cncrng ax-mp a1i id gsumsn syl3anc mulid2 cr 1z eqtrd ex
        cn0 wn cpr cdif cfv wf wf1o f1oi f1of syl6ss zsscn fss sylancr ccnv cfn
        cima ssfi cdm cnvimass dmresi sseqtri cin c0 cun rcla4v syl2anc resabs1
        oveq1 oveq2i adantl gsumsubmcl syl5ss sseldd wo syl5eq wne sylnib eqcom
        wb ovex bitri 3eqtrd cdivides wbr fzm1ndvds eqid elfznn sylibr cc0 elpr
        orcom eldif sylanbrc cneg c2 cpw snssd eqssd syl6eqr cprime nncn negsub
        prmnn renegcli recni addcom eqtr3d ccmn crngmgp nnm1nn0 cnfldbas mgpbas
        nn0cn 3eqtr4d crp nnrp modcyc wex nss cnfld1 rngidval cnfldmul mgpplusg
        c0g elpwi elfzelz ssriv cvv fzfi disjdif undif2 simprl eleq1d prssg ibi
        sylc ssequn1 syl5req gsumsplit difss oveq12d prfi sstrd mp2an zre dfsn2
        zmulcl orcd wilthlem1 biimpar syldan preq2d 3eqtr3d reseq2i eqtr4i snfi
        df-pr elsni eqeltrd adantlr simprr elsn biorf 3bitr4g bitr4d necon3abid
        elsnc biimpa eqidd prmdiv nnmulcl nnz moddvds mpbird pm2.61dane modmul1
        gsumunsn syl221anc sseldi wpss wi elexi elpw2 cuz syl6eleq eluzfz2 prmz
        nn0uz zsubcl dvdsmul1 subdir mulcl subsub eqcomd subdi mulid1 zcn pncan
        3eqtr2d breqtrrd prmdiveq mpbi2and prmdivdiv eqeq12d syl5ibr mtod ioran
        jca sylnibr eldifi r19.21bi sylan2 sselda syl5ibcom orim12d vex 3imtr4g
        eqeq2d con3d impr sylan2b ralrimiva dfss1 prnz eqnetrd disj4 necon2abii
        psseq1 reseq2 eqeq1d imbi12d syl3c exlimdv syl5bi pm2.61d ) AFENUCOZUDZ
        UEZGUFFUGZUHOZEPOZNUUAZEPOZQZAVWEVWKAVWEUIZVWHGUAVWDUAUJZUKZUHOZEPOZVWJ
        VWLVWGVWOEPVWLVWFVWNGUHVWLVWFUFVWDUGVWNVWLFVWDUFVWLFVWDAVWEULAVWDFUEVWE
        AVWCFAVWCFRZCUJZEUUBUCOZUMOZEPOZFRZCFUNZAFNVWCUOOZUUCZRZVWQVXCUIZAFDRZV
        XFVXGUILVWCBUJZRZVXAVXIRZCVXIUNZUIZVXGBFVXEDVXIFQVXJVWQVXLVXCVXIFVWCUPV
        XKVXBCVXIFVXIFVXAUPUQURJUSUTZVAZVBZUUDVCUUEVDUAVWDVEUUFVFVGAVWPVWJQVWEA
        VWPVWINEVHOZVIOZEPOZVWJAVWOVXREPAVWCVWIEVIOZVWOVXRAEVWIVIOZVWCVXTAESRZN
        SRZVYAVWCQAEVNRZVYBAEUUGRZVYDKEUUJZVJZEUUHZVJZVKENUUIVLAVYBVWISRVYAVXTQ
        VYIVWINVMUUKZUULEVWIUUMVLUUNAGUUORZVWCSRZVYLVWOVWCQVYKAVOVPRZVYKVQVOGIU
        UPVRZVSAVWCWHRZVYLAVYDVYOVYGEUUQZVJVWCUUTZVJZVYRVWMSVWCUAGVWCSSVOGIUURU
        USZVWMVWCQZVTWAWBAVXQEVWIVIAVYBVXQEQVYIEWCVJVFUVAVGAVWIWDRZEUVBRZNTRZVX
        SVWJQWUAAVYJVSAVYDWUBVYGEUVCVJZWUCAWEVSVWIENUVDWBWFVCWFWGVWEWIVWMFRZVWM
        VWDRZWIZUIZUAUVEAVWKUAFVWDUVFAWUHVWKUAAWUHVWKAWUHUIZVWHGUFVWMVWMVWSUMOZ
        EPOZWJZUGZUHOZGUFFWULWKZUGZUHOZVHOZEPOZNWUQVHOZEPOZVWJWUIVWGWUREPWUIVWG
        GVWFWULUGZUHOZGVWFWUOUGZUHOZVHOWURWUIFSWULWUOVHVWFGDNVYSVYMNGUVKWLQVQVO
        NGVPIUVGUVHVRZVOVHGIUVIUVJZVYKWUIVYNVSZAVXHWUHLVCAFSVWFWMZWUHAFFVWFWMZF
        SUEZWVIFFVWFWNWVJFWOFFVWFWPVRAFTSAFVXDTAVXFFVXDUEZAVXFVXGVXNVBFVXDUVLVJ
        ZUAVXDTVWMNVWCUVMZUVNWQZWRWQZFFSVWFWSWTVCAVWFXAUVONUDWKZXCZXBRZWUHAFXBR
        ZWVRFUEWVSAVXDXBRZWVLWVTNVWCUVPZWVMVXDFXDWTWVRVWFXEFVWFWVQXFFXGXHFWVRXD
        VLVCWULWUOXIXJQWUIWULFUVQVSWUIWULWUOXKWULFXKZFWULFUVRWUIWULFUEZWWCFQWUI
        WUEWUKFRZWWDAWUEWUGUVSZWUIWUEVXCWWEWWFAVXCWUHAVWQVXCVXOVAVCZVXBWWECVWMF
        VWRVWMQZVXAWUKFWWHVWTWUJEPVWRVWMVWSUMXOVGUVTXLUWCZWUEWWEUIWWDVWMWUKFFFU
        WAUWBXMZWULFUWDUTUWEUWFWUIWVCWUNWVEWUQVHWUIWVBWUMGUHWUIWWDWVBWUMQWWJUFW
        ULFXNVJVFWVEWUQQWUIWVDWUPGUHWUOFUEWVDWUPQFWULUWGZUFWUOFXNVRXPVSUWHWFVGW
        UIWUNWDRZNWDRZWUQTRWUBWUNEPOZNEPOZQZWUSWVAQWUIWUNTRWWLWUIBCWULSVHTWUMGX
        BNWVFWVGVYSWVHWULXBRZWUIVWMWUKUWIZVSTSUEWUIWRVSZWUCWUIWEVSZVXITRVWRTRUI
        VXIVWRVHOTRWUIVXIVWRUWNXQZWUIWULWULWUMWMZWULTUEWULTWUMWMWULWULWUMWNWXBW
        ULWOWULWULWUMWPVRWUIWULFTWWJAFTUEWUHWVOVCZUWJWULWULTWUMWSWTWUMXAWVQXCZX
        BRZWUIWWQWXDWULUEWXEWWRWXDWUMXEWULWUMWVQXFWULXGXHWULWXDXDUWKVSXRWUNUWLV
        JWWMWUIVMVSWUIBCWUOSVHTWUPGXBNWVFWVGVYSWVHWUIWWAWUOVXDUEZWUOXBRZWWBWUIW
        UOFVXDWWKAWVLWUHWVMVCZXSZVXDWUOXDWTZWWSWWTWXAWUIWUOWUOWUPWMZWUOTUEWUOTW
        UPWMWUOWUOWUPWNWXKWUOWOWUOWUOWUPWPVRWUIWUOFTWWKWXCXSWUOWUOTWUPWSWTWUIWX
        GWUPXAWVQXCZWUOUEWXLXBRWXJWXLWUPXEWUOWUPWVQXFWUOXGXHWUOWXLXDVLXRZAWUBWU
        HWUDVCWUIWWPVWMNWUIVWMNQZUIZWUNNEPWXOGUBVWMUDZUBUJZUKZUHOZVWMWUNNWUIWXS
        VWMQZWXNWUIVYKVWMSRZWYAWXTWVHWUIFSVWMAWVKWUHWVPVCZWWFXTZWYCWXQSVWMUBGVW
        MSVYSWXQVWMQZVTWAWBZVCWXOWXRWUMGUHWXOWXRUFWXPUGWUMUBWXPVEWXOWXPWULUFWXO
        WXPVWMVWMWJWULVWMUWMWXOVWMWUKVWMWUIWXNWXNVYTYAZVWMWUKQZWXOWXNVYTWUIWXNU
        LZUWOWUIWYGWYFWUIVYEVWMVXDRZWYGWYFYFAVYEWUHKVCZWUIFVXDVWMWXHWWFXTZEVWMU
        WPXMZUWQUWRUWSYBVDYBVFWYHUWTVGWUIVWMNYCZUIZWWNVWMWUKVHOZEPOZWWOWYNWUNWY
        OEPWYNWUNWXSWUKVHOZWYOWYOWYNWUNGUBWXPWUKUDXKZWXQUKZUHOWYQWUMWYSGUHWUMUF
        WYRUGWYSWULWYRUFVWMWUKUXDUXAUBWYRVEUXBXPWYNWXPSVHUBGWUKSWXQWUKVYSWVGVYK
        WYNVYNVSWXPXBRWYNVWMUXCVSWUIWXQWXPRZWXQSRWYMWUIWYTUIWXQVWMSWYTWYDWUIWXQ
        VWMUXEXQWUIWYAWYTWYCVCUXFUXGWUIWUKSRWYMWUIFSWUKWYBWWIXTVCZWUIWYMWUKWXPR
        ZWIWUIXUBVWMNWUIWXNVYTWXNYAZXUBWUIVYTWIWXNXUCYFWUIWUFVYTAWUEWUGUXHZUAVW
        CUXIZYDVYTWXNUXJVJWUIWYGWYFXUBXUCWYLXUBWUKVWMQWYGWUKVWMWUJEPYGUXNWUKVWM
        YEYHVYTWXNYRUXKUXLUXMUXOXUAWXQWUKQVTUYDYBWYNWXSVWMWUKVHWUIWXTWYMWYEVCVG
        WYNWYOUXPYIVGWUIWYPWWOQZWYMWUIXUFEWYONUCOYJYKZWUIWUKVXDRZXUGWUIVYEVWMTR
        ZEVWMYJYKWIZXUHXUGUIWYJWUIWYIXUIWYKWVNVJWUIVYDWYIXUJWUIVYEVYDWYJVYFVJZW
        YKEVWMYLXMVWMEWUKWUKYMZUXQWBVAWUIVYDWYOTRZWUCXUFXUGYFAVYDWUHVYGVCWUIWYO
        VNRZXUMWUIVWMVNRZWUKVNRZXUNWUIWYIXUOWYKVWMVWCYNVJWUIXUHXUPWUIFVXDWUKWXH
        WWIXTWUKVWCYNVJVWMWUKUXRXMWYOUXSVJWWTWYONEUXTWBUYAVCWFUYBWUNNWUQEUYCUYE
        WUIWVAWUQEPOZVWJWUIWUTWUQEPWUIWUQSRWUTWUQQWUITSWUQWRWXMUYFWUQWCVJVGWUIW
        UODRZHUJZFUYGZGUFXUSUGZUHOZEPOZVWJQZUYHZHDUNZWUOFUYGZXUQVWJQZWUIWUOVXER
        ZVWCWUORZVXAWUORZCWUOUNZUIZXURWUIWXFXVIWXIWUOVXDVXDXBWWBUYIUYJYOWUIXVJX
        VLWUIVWQVWCWULRZWIXVJAVWQWUHVXPVCZWUIVWCVWMQZVWCWUKQZYAZXVNWUIXVPWIZXVQ
        WIZUIXVRWIWUIXVSXVTWUIWUFXVPXUDWUFVYTXVPXUEVWMVWCYEYHYDZWUIXVQXVPXWAXVQ
        XVPWUIVWCVWSUMOZEPOZWUKVWSUMOZEPOZQXVQXWBXWDEPVWCWUKVWSUMXOVGWUIVWCXWCV
        WMXWEWUIVWCYPVWCUOORZEVWCVWCVHOZNUCOZYJYKZVWCXWCQZWUIVWCYPUYKWLZRXWFWUI
        VWCWHXWKWUIVYDVYOXUKVYPVJZUYOUYLYPVWCUYMVJWUIEEVWCNUCOZVHOZXWHYJWUIETRZ
        XWMTRZEXWNYJYKWUIVYEXWOWYJEUYNVJWUIVWCTRZWUCXWPWUIFTVWCWXCXVOXTZWEVWCNU
        YPVLZEXWMUYQXMWUIXWHXWNNVIOZNUCOZXWNWUIXWGXWTNUCWUIXWGEVWCVHOZNVWCVHOZU
        COZXWTWUIVYBVYCVYLXWGXXDQWUIVYDVYBXUKVYHVJZVYCWUIVKVSZWUIVYOVYLXWLVYQVJ
        ZENVWCUYRWBWUIXXDXXBVWCUCOZXXBEUCOZNVIOZXWTWUIXXCVWCXXBUCWUIVYLXXCVWCQX
        XGVWCWCVJVFWUIXXHXXJWUIXXBSRZVYBVYCXXHXXJQWUIVYBVYLXXKXXEXXGEVWCUYSXMXX
        EXXFXXBENUYTWBVUAWUIXWTXXJWUIXWNXXINVIWUIXWNXXBENVHOZUCOZXXIWUIVYBVYLVY
        CXWNXXMQXXEXXGXXFEVWCNVUBWBWUIXXLEXXBUCWUIVYBXXLEQXXEEVUCVJVFWFVGVUAVUF
        WFVGWUIXWNSRZVYCXXAXWNQWUIVYBXWMSRZXXNXXEWUIXWPXXOXWSXWMVUDVJEXWMUYSXMV
        KXWNNVUEVLWFVUGWUIVYEXWQEVWCYJYKWIZXWFXWIUIXWJYFWYJXWRWUIVYDVWCVXDRXXPX
        UKWUIFVXDVWCWXHXVOXTEVWCYLXMVWCEXWCVWCXWCYMVUHWBVUIWUIVYEWYIVWMXWEQZWYJ
        WYKVWMEWUKXULVUJXMZVUKVULVUMVUOXVPXVQVUNYOVWCVWMWUKENUCYGYQVUPVWCFWULYS
        YTWUIXVKCWUOWUIVWRWUORZUIVXBVXAWULRZWIZXVKXXSWUIVWRFRZVXBVWRFWULVUQWUIV
        XBCFWWGVURVUSXXSWUIXYBVWRWULRZWIZUIXYAVWRFWULYSWUIXYBXYDXYAWUIXYBUIZXXT
        XYCXYEVXAVWMQZVXAWUKQZYAVWRWUKQZWWHYAZXXTXYCXYEXYFXYHXYGWWHXYEVWRVXAVWS
        UMOZEPOZQZXYFXYHXYEVYEVWRVXDRXYLWUIVYEXYBWYJVCWUIFVXDVWRWXHVUTVWREVXAVX
        AYMVUJXMZXYFXYKWUKVWRXYFXYJWUJEPVXAVWMVWSUMXOVGVVEVVAXYGWWHXYEXYKXWEQXY
        GXYJXWDEPVXAWUKVWSUMXOVGXYEVWRXYKVWMXWEXYMWUIXXQXYBXXRVCVUKVULVVBVXAVWM
        WUKVWTEPYGYQXYCWWHXYHYAXYIVWRVWMWUKCVVCYQWWHXYHYRYHVVDVVFVVGVVHVXAFWULY
        SYTVVIVUOVXMXVMBWUOVXEDVXIWUOQVXJXVJVXLXVLVXIWUOVWCUPVXKXVKCVXIWUOVXIWU
        OVXAUPUQURJUSYTAXVFWUHMVCWUIFWULXIZXJYCXVGWUIXYNWULXJWUIWWDXYNWULQWWJWU
        LFVVJUTWULXJYCWUIVWMWUKUAVVCVVKVSVVLXVGXYNXJFWULVVMVVNYOXVEXVGXVHUYHHWU
        ODXUSWUOQZXUTXVGXVDXVHXUSWUOFVVOXYOXVCXUQVWJXYOXVBWUQEPXYOXVAWUPGUHXUSW
        UOUFVVPVFVGVVQVVRXLVVSWFYIWGVVTVWAVWB $.
        $( [24-Jan-2015] $)
    $}

    $( Lemma for ~ wilth .  Here we round out the argument of ~ wilthlem2 with
       the final step of the induction.  The induction argument shows that
       every subset of ` 1 ... ( P - 1 ) ` that is closed under inverse and
       contains ` P - 1 ` multiplies to ` -u 1 mod P ` , and clearly
       ` 1 ... ( P - 1 ) ` itself is such a set.  Thus the product of all the
       elements is ` -u 1 ` , and all that is left is to translate the group
       sum notation (which we used for its unordered summing capabilities) into
       an ordered sequence to match the definition of the factorial. $)
    wilthlem3 $p |- ( P e. Prime -> P || ( ( ! ` ( P - 1 ) ) + 1 ) ) $=
      ( vs wcel cid c1 co cfv cmo wceq cmul cz wi a1i cc vt vk cprime cmin cres
      cfz cgsu cneg cfa caddc cdivides wbr cv c2 cexp cuz cn prmuz2 uz2m1nn syl
      wral nnuz syl6eleq eluzfz2 wa wn simpl elfzelz prmnn fzm1ndvds sylan eqid
      adantl prmdiv syl3anc simpld ralrimiva cpw cfn fzfi elexi pwid raleqbi1dv
      eleq2 anbi12d elrab2 mpbiran sylanbrc reseq2 oveq2d oveq1d eqeq1d imbi12d
      eleq1 imbi2d wpss bi2.04 pm2.27 syl6ib syl5bi alimdv df-ral syl6ibr com12
      wal w3a simp1 simp3 simp2 wilthlem2 3exp sylcom findcard3 ax-mp wb ccnfld
      mpd ccrg cncrng cnfld1 rngidval cnfldmul mgpplusg cnfldbas mgpbas crngmgp
      c0g ccmn wss zsscn 1z zmulcl wf wf1o fss mp2an ccnv cima cseq idi cvv csn
      f1oi f1of ssriv cdif cnvimass fdmi sseqtri ssfi gsumsubmcl znegcl moddvds
      cdm mpbid ccom fcoi1 fveq1i fvres syl5eq seqfveq cbs cplusg wf1 f1of1 crn
      wfo f1ofo forn mp2b sseqtr4i gsumval3 facnn 3eqtr4d cn0 faccl nncn ax-1cn
      nnm1nn0 subneg sylancl eqtrd breqtrd ) DUCIZDEJKDKUDLZUFLZUEZUGLZKUHZUDLZ
      UWEUIMZKUJLZUKUWDUWHDNLZUWIDNLZOZDUWJUKULZUWDUWFCIZUWOUWDUWEUWFIZBUMZDUNU
      DLUOLDNLZUWFIZBUWFVAZUWQUWDUWEKUPMZIUWRUWDUWEUQUXCUWDDUNUPMIUWEUQIZDURDUS
      UTZVBVCZKUWEVDUTUWDUXABUWFUWDUWSUWFIZVEZUXADUWSUWTPLKUDLUKULZUXHUWDUWSQIZ
      DUWSUKULVFZUXAUXIVEUWDUXGVGUXGUXJUWDUWSKUWEVHZVMUWDDUQIZUXGUXKDVIZDUWSVJV
      KUWSDUWTUWTVLVNVOVPVQUWQUWFUWFVRZIUWRUXBVEZUWFUWFVSKUWEVTZWAWBUWEAUMZIZUW
      TUXRIZBUXRVAZVEUXPAUWFUXOCUXRUWFOUXSUWRUYAUXBUXRUWFUWEWDUXTUXABUXRUWFUXRU
      WFUWTWDWCWEGWFWGWHUWFVSIZUWDUWQUWORZRZUXQUWDHUMZCIZEJUYEUEZUGLZDNLZUWNOZR
      ZRZUWDUAUMZCIZEJUYMUEZUGLZDNLZUWNOZRZRZUYDHUAUWFUYEUYMOZUYKUYSUWDVUAUYFUY
      NUYJUYRUYEUYMCWNVUAUYIUYQUWNVUAUYHUYPDNVUAUYGUYOEUGUYEUYMJWIWJWKWLWMWOUYE
      UWFOZUYKUYCUWDVUBUYFUWQUYJUWOUYEUWFCWNVUBUYIUWMUWNVUBUYHUWHDNVUBUYGUWGEUG
      UYEUWFJWIWJWKWLWMWOUYEUYMWPZUYLRZHXEZUYTRUYMVSIVUEUWDVUCUYJRZHCVAZUYSUWDV
      UEVUGUWDVUEUYFVUFRZHXEVUGUWDVUDVUHHVUDUWDVUCUYKRZRZUWDVUHVUCUWDUYKWQUWDVU
      JVUIVUHUWDVUIWRVUCUYFUYJWQWSWTXAVUFHCXBXCXDUWDVUGUYNUYRUWDVUGUYNXFABCDUYM
      EHFGUWDVUGUYNXGUWDVUGUYNXHUWDVUGUYNXIXJXKXLSXMXNXQUWDUXMUWHQIUWIQIZUWOUWP
      XOUXNUWDABUWFTPQUWGEVSKXPXRIZKEYGMOZXSXPKEXRFXTYAXNZXPPEFYBYCZTXPEFYDYEZE
      YHIZUWDVULVUQXSXPEFYFXNSZUYBUWDUXQSZQTYIZUWDYJSKQIZUWDYKSZUXRQIUXJVEUXRUW
      SPLQIUWDUXRUWSYLVMUWFQUWGYMZUWDUWFUWFUWGYMZUWFQYIVVCUWFUWFUWGYNZVVDUWFUUC
      ZUWFUWFUWGUUDXNZBUWFQUXLUUEUWFUWFQUWGYOYPZSUWGYQUUAKUUBUUFZYRZVSIZUWDUYBV
      VJUWFYIVVKUXQVVJUWGUUNUWFUWGVVIUUGUWFUWFUWGVVGUUHUUIZUWFVVJUUJYPSUUKUWDVV
      AVUKVVBKUULUTUWHUWIDUUMVOUUOUWDUWJUWKUWIUDLZUWLUWDUWHUWKUWIUDUWDUWEPUWGUW
      GUUPZKYSMUWEPJKYSMZUWHUWKUWDPUBVVNJKUWEUXFUBUMZUWFIZVVPVVNMZVVPJMZOUWDVVQ
      VVRVVPUWGMVVSVVPVVNUWGVVDVVNUWGOVVGUWFUWFUWGUUQXNUURVVPUWFJUUSUUTVMUVAUWD
      UWFTPUWGEUWGUWEVSVVNYQVVIYRZKTEUVBMOVUPYTVUMVUNYTPEUVCMOVUOYTUWDVUQRVURYT
      UWDUYBRVUSYTUWFTUWGYMZUWDVVCVUTVWAVVCVVHYTVUTYJYTUWFQTUWGYOYPSUXEUWFUWFUW
      GUVDZUWDVVEVWBVVFUWFUWFUWGUVEXNSVVJUWGUVFZYIUWDVVJUWFVWCVVLVVEUWFUWFUWGUV
      GVWCUWFOVVFUWFUWFUWGUVHUWFUWFUWGUVIUVJUVKSVVTVLUVLUWDUXDUWKVVOOUXEUWEUVMU
      TUVNWKUWDUWKTIZKTIVVMUWLOUWDUWKUQIZVWDUWDUWEUVOIZVWEUWDUXMVWFUXNDUVSUTUWE
      UVPUTUWKUVQUTUVRUWKKUVTUWAUWBUWC $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d p x y z N $.
    $( Wilson's theorem.  A number is prime iff it is greater or equal to ` 2 `
       and ` ( N - 1 ) ! ` is congruent to ` -u 1 ` , ` mod N ` , or
       alternatively if ` N ` divides ` ( N - 1 ) ! + 1 ` .  In this part of
       the proof we show the relatively simple reverse implication; see
       ~ wilthlem3 for the forward implication.  (Contributed by Mario
       Carneiro, 24-Jan-2015.) $)
    wilth $p |- ( N e. Prime <-> ( N e. ( ZZ>= ` 2 ) /\
      N || ( ( ! ` ( N - 1 ) ) + 1 ) ) ) $=
      ( vx vy vp cprime wcel c2 cfv c1 co cdivides wbr wa cv cexp cn clt cz syl
      wral vz cuz cmin cfa caddc prmuz2 cmo cfz cpw crab ccnfld cmgp eqid eleq2
      wceq oveq1 oveq1d eleq1d cbvralv raleqbi1dv anbi12d cbvrabv wilthlem3 jca
      syl5bb cle wn wi simpl ad2antrl eluz2b2 sylib simpld nnz cn0 nnm1nn0 nn0z
      simpll cr nnre nnsqcl cc recnd 1z a1i 2z simprd expordi syl32anc eqbrtrrd
      exp1 1lt2 simprr ltletrd wb zltlem1 syl2anc mpbid eluz2 syl3anbrc dvdsfac
      faccl 3syl ndvdsp1 mpd simplr peano2zdi dvdstr mpan2d mtod expr ralrimiva
      syl3anc isprm5 sylanbrc impbii ) AEFZAGUBHZFZAAIUCJZUDHZIUEJZKLZMZXQXSYCA
      UFBCXTUANZFZDNZAGUCJZOJZAUGJZYEFZDYETZMZUAIXTUHJUIZUJAUKULHZYOUMYMXTBNZFZ
      CNZYHOJZAUGJZYPFZCYPTZMUABYNYEYPUOZYFYQYLUUBYEYPXTUNYLYTYEFZCYETUUCUUBYKU
      UDDCYEYGYRUOZYJYTYEUUEYIYSAUGYGYRYHOUPUQURUSUUDUUACYEYPYEYPYTUNUTVEVAVBVC
      VDYDXSYGGOJZAVFLZYGAKLZVGZVHZDETXQXSYCVIYDUUJDEYDYGEFZUUGUUIYDUUKUUGMZMZU
      UHYGYBKLZUUMYGYAKLZUUNVGZUUMYGPFZXTYGUBHFZUUOUUMUUQIYGQLZUUMYGXRFZUUQUUSM
      UUKUUTYDUUGYGUFVJYGVKVLZVMZUUMYGRFZXTRFZYGXTVFLZUURUUMUUQUVCUVBYGVNSZUUMX
      TVOFZUVDUUMAPFZUVGUUMUVHIAQLZUUMXSUVHUVIMXSYCUULVRAVKVLVMZAVPSZXTVQSUUMYG
      AQLZUVEUUMYGUUFAUUMUUQYGVSFZUVBYGVTSZUUMUUFPFZUUFVSFUUMUUQUVOUVBYGWASUUFV
      TSUUMUVHAVSFUVJAVTSUUMYGIOJZYGUUFQUUMYGWBFUVPYGUOUUMYGUVNWCYGWKSUUMUVMIRF
      ZGRFZUUSIGQLZUVPUUFQLUVNUVQUUMWDWEUVRUUMWFWEUUMUUQUUSUVAWGZUVSUUMWLWEYGIG
      WHWIWJYDUUKUUGWMWNUUMUVCARFZUVLUVEWOUVFUUMUVHUWAUVJAVNSZYGAWPWQWRYGXTWSWT
      YGXTXAWQUUMYARFZUUQUUSUUOUUPVHUUMUVGYAPFUWCUVKXTXBYAVNXCZUVBUVTYGYAXDXMXE
      UUMUUHYCUUNXSYCUULXFUUMUVCUWAYBRFUUHYCMUUNVHUVFUWBUUMYAUWDXGYGAYBXHXMXIXJ
      XKXLDAXNXOXP $.
      $( [24-Jan-2015] $)
  $}

