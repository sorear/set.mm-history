$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Univariate Polynomials
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c PwSer1 $. $( Univariate power series $)
  $c var1 $. $( Univariate power series variable $)
  $c Poly1 $. $( Univariate polynomial $)
  $c evalSub1 $. $( Evaluation in a subring $)
  $c eval1 $. $( Evaluation of a univariate polynomial $)
  $c deg1 $. $( Univariate polynomial degree $)
  $c coe1 $. $( Multivariate -> univariate $)
  $c toPoly1 $. $( Univariate -> multivariate $)
  $c Monic1 $. $( Monic polynomial $)

  $( Univariate power series. $)
  cps1 $a class PwSer1 $.

  $( The base variable of a univariate power series. $)
  cv1 $a class var1 $.

  $( Univariate polynomials. $)
  cpl1 $a class Poly1 $.

  $( Evaluation in a subring. $)
  ces1 $a class evalSub1 $.

  $( Evaluation of a univariate polynomial. $)
  ce1 $a class eval1 $.

  $( Univariate polynomial degree. $)
  cdg1 $a class deg1 $.

  $( Convert a multivariate polynomial representation to univariate. $)
  cco1 $a class coe1 $.

  $( Convert a univariate polynomial representation to multivariate. $)
  ctp1 $a class toPoly1 $.

  $( Monic polynomials. $)
  cmn1 $a class Monic1 $.

  ${
    $d b f n r s x y $.
    $( Define the algebra of univariate power series. $)
    df-psr1 $a |- PwSer1 = ( r e. _V |-> ( ( 1o ordPwSer r ) ` (/) ) ) $.

    $( Define the base element of a univariate power series (the ` X ` element
       of the set ` R [ X ] ` of polynomials and also the ` X ` in the set
       ` R [ [ X ] ] ` of power series). $)
    df-vr1 $a |- var1 = ( r e. _V |-> ( ( 1o mVar r ) ` (/) ) ) $.

    $( Define the algebra of univariate polynomials. $)
    df-ply1 $a |- Poly1 = ( r e. _V |->
      ( ( PwSer1 ` r ) |`s ( Base ` ( 1o mPoly r ) ) ) ) $.

    $( Define the evaluation map for the univariate polynomial algebra.  The
       function ` ( S evalSub1 R ) : V --> ( S ^m S ) ` makes sense when ` S `
       is a ring and ` R ` is a subring of ` S ` , and where ` V ` is the set
       of polynomials in ` ( Poly1 `` R ) ` .  This function maps an element of
       the formal polynomial algebra (with coefficients in ` R ` ) to a
       function from assignments to the variable from ` S ` into an element of
       ` S ` formed by evaluating the polynomial with the given assignment. $)
    df-evls1 $a |- evalSub1 = ( s e. _V , r e. ~P ( Base ` s ) |->
      [_ ( Base ` s ) / b ]_ ( ( x e. ( b ^m ( b ^m 1o ) ) |-> ( x o.
        ( y e. b |-> ( 1o X. { y } ) ) ) ) o. ( ( 1o evalSub s ) ` r ) ) ) $.

    $( A simplication of ` evalSub1 ` when the evaluation ring is the same as
       the coefficient ring. $)
    df-evl1 $a |- eval1 = ( r e. _V |-> ( r evalSub1 ( Base ` r ) ) ) $.

    $( Define the degree of a univariate polynomial. $)
    df-deg1 $a |- deg1 = ( r e. _V , f e. ( Poly1 ` r ) |->
      ( ( 1o mDeg r ) ` f ) ) $.

    $( Define the coefficient function for a univariate polynomial. $)
    df-coe1 $a |- coe1 = ( f e. _V |-> ( n e. NN0 |->
      ( f ` ( 1o X. { n } ) ) ) ) $.

    $( Define a function which maps a coefficient function for a univariate
       polynomial to the corresponding polynomial object. $)
    df-toply1 $a |- toPoly1 = ( f e. _V |-> ( n e. ( NN0 ^m 1o ) |->
      ( f ` ( n ` (/) ) ) ) ) $.

    $( Define the set of monic univariate polynomials. $)
    df-mon1 $a |- Monic1 = ( r e. _V |-> { f e. ( Poly1 ` r ) |
      ( ( coe1 ` f ) ` ( r deg1 f ) ) = ( 1r ` r ) } ) $.
  $}


  $( The set of finite bags on ` 1o ` is just the set of all functions from
     ` 1o ` to ` NN0 ` . $)
  psr1baslem $p |- ( NN0 ^m 1o ) =
    { f e. ( NN0 ^m 1o ) | ( `' f " NN ) e. Fin } $=
    ( cn0 c1o cmap co cv ccnv cn cima cfn wcel crab wceq wral wss c0 df1o2 snfi
    csn eqeltri cdm cnvimass wf cvv elexi elmapi mpan fdm syl5sseq ssfi sylancr
    syl rgen rabid2 mpbir ) BCDEZAFZGHIZJKZAUPLMUSAUPNUSAUPUQUPKZCJKURCOUSCPSJQ
    PRTZUTUQUAZURCUQHUBUTCBUQUCZVBCMCUDKUTVCCJVAUEUQBCUFUGCBUQUHULUICURUJUKUMUS
    AUPUNUO $.
    $( [9-Feb-2015] $)

  ${
    $d r R $.
    psr1val.1 $e |- S = ( PwSer1 ` R ) $.
    $( Value of the ring of univariate power series. $)
    psr1val $p |- ( R e. V -> S = ( ( 1o ordPwSer R ) ` (/) ) ) $=
      ( vr wcel cvv c0 c1o copws co wceq elex cps1 cv oveq2 fveq1d df-psr1 fvex
      cfv fvmpt syl5eq syl ) ACFAGFZBHIAJKZTZLACMUDBANTUFDEAHIEOZJKZTUFGNUGALHU
      HUEUGAIJPQERHUESUAUBUC $.
      $( [8-Feb-2015] $)

    $( The ring of univariate power series is a commutative ring. $)
    psr1crng $p |- ( R e. CRing -> S e. CRing ) $=
      ( ccrg wcel c0 c1o copws co cfv psr1val con0 eqid 1on a1i id cxp opsrcrng
      wss 0ss eqeltrd ) ADEZBFGAHIJZDABDCKUBAFGUCLUCMGLEUBNOUBPFGGQZSUBUDTORUA
      $.
      $( [8-Feb-2015] $)

    $( The ring of univariate power series is an associative algebra. $)
    psr1assa $p |- ( R e. CRing -> S e. AssAlg ) $=
      ( ccrg wcel c0 c1o copws co cfv casa psr1val con0 eqid 1on a1i id cxp wss
      0ss opsrassa eqeltrd ) ADEZBFGAHIJZKABDCLUCAFGUDMUDNGMEUCOPUCQFGGRZSUCUET
      PUAUB $.
      $( [8-Feb-2015] $)

    psr1bas.k $e |- K = ( Base ` R ) $.
    psr1bas.b $e |- B = ( Base ` S ) $.
    $( The base set of the ring of univariate power series. $)
    psr1bas $p |- ( R e. V -> B = ( K ^m ( NN0 ^m 1o ) ) ) $=
      ( vf wcel c0 c1o copws co cfv cbs cmap con0 eqid a1i cmps cn0 psr1val 1on
      fveq2d syl5eq id cxp wss 0ss opsrbas psr1baslem psrbas 3eqtr2d ) BEJZAKLB
      MNOZPOZLBUANZPOZDUBLQNZQNUOACPOUQHUOCUPPBCEFUCUEUFUOBURKLUPREURSZUPSLRJUO
      UDTZUOUGZKLLUHZUIUOVDUJTUKUOUSUTBURILDREVAGIULUSSVBVCUMUN $.
      $( [8-Feb-2015] $)
  $}

  ${
    $d r R $.
    vr1val.1 $e |- X = ( var1 ` R ) $.
    $( The value of the generator of the power series algebra (the ` X ` in
       ` R [ [ X ] ] ` ). $)
    vr1val $p |- ( R e. V -> X = ( ( 1o mVar R ) ` (/) ) ) $=
      ( vr wcel cvv c0 c1o cmvr co cfv wceq elex cv1 oveq2 fveq1d df-vr1 fvex
      cv fvmpt syl5eq syl ) ABFAGFZCHIAJKZLZMABNUDCAOLUFDEAHIETZJKZLUFGOUGAMHUH
      UEUGAIJPQERHUESUAUBUC $.
      $( [8-Feb-2015] $)

    vr1cl2.2 $e |- S = ( PwSer1 ` R ) $.
    vr1cl2.3 $e |- B = ( Base ` S ) $.
    $( The variable ` X ` is a member of the power series algebra
       ` R [ [ X ] ] ` . $)
    vr1cl2 $p |- ( R e. Ring -> X e. B ) $=
      ( crg wcel c0 c1o cmvr co cfv vr1val cmps cbs con0 eqid a1i 1on id mvrcl2
      0lt1o copws psr1val fveq2d syl5eq cxp wss opsrbas eqtr4d eleqtrrd eqeltrd
      0ss ) BHIZDJKBLMZNZABHDEOUPURKBPMZQNZAUPUTBUSKUQRJUSSZUQSUTSKRIUPUATZUPUB
      ZJKIUPUDTUCUPAJKBUEMNZQNZUTUPACQNVEGUPCVDQBCHFUFUGUHUPBUSJKVDRHVAVDSVBVCJ
      KKUIZUJUPVFUOTUKULUMUN $.
      $( [8-Feb-2015] $)
  $}

  ${
    $d r x y R $.  $d r S $.
    ply1val.1 $e |- P = ( Poly1 ` R ) $.
    ${
      ply1val.2 $e |- S = ( PwSer1 ` R ) $.
      $( The value of the set of univariate polynomials. $)
      ply1val $p |- ( R e. V -> P = ( S |`s ( Base ` ( 1o mPoly R ) ) ) ) $=
        ( vr wcel cvv c1o cmpl co cbs cfv cress wceq elex cpl1 cv cps1 ovex syl
        fveq2 syl6eqr oveq2 fveq2d oveq12d df-ply1 fvmpt syl5eq ) BDHBIHZACJBKL
        ZMNZOLZPBDQUKABRNUNEGBGSZTNZJUOKLZMNZOLUNIRUOBPZUPCURUMOUSUPBTNCUOBTUCF
        UDUSUQULMUOBJKUEUFUGGUHCUMOUAUIUJUB $.
        $( [9-Feb-2015] $)

      ply1bas.u $e |- U = ( Base ` P ) $.
      $( The value of the set of univariate polynomials. $)
      ply1bas $p |- ( R e. V -> U = ( Base ` ( 1o mPoly R ) ) ) $=
        ( wcel cbs cfv c1o co fveq2d wss con0 eqid a1i c0 eqtr4d cmpl wceq cmps
        cress ply1val 1on id mplbasss copws cxp 0ss opsrbas psr1val sseqtrd syl
        ressbas2 syl5eq ) BEIZDAJKZLBUAMZJKZHURUSCVAUDMZJKZVAURAVBJABCEFGUENURV
        ACJKZOVAVCUBURVALBUCMZJKZVDURVFUTBVEVALPEUTQVEQZVAQLPIURUFRZURUGZVFQUHU
        RVFSLBUIMKZJKVDURBVESLVJPEVGVJQVHVISLLUJZOURVKUKRULURCVJJBCEGUMNTUNVAVD
        VBCVBQVDQUPUOTUQ $.
        $( [9-Feb-2015] $)

      $( The value of the set of univariate polynomials. $)
      ply1lss $p |- ( R e. Ring -> U e. ( LSubSp ` S ) ) $=
        ( vx vy crg wcel c1o co cbs cfv clss con0 eqid a1i c0 cmpl ply1bas cmps
        1on id mpllss copws eqidd cxp wss 0ss opsrbas cv cplusg opsrplusg oveqd
        wa adantr psrsca opsrsca cvsca opsrvsca psrlmod lsspropd psr1val fveq2d
        wceq eqtr4d eleqtrd eqeltrd ) BJKZDLBUAMZNOZCPOZABCDJEFGUBVKVMLBUCMZPOZ
        VNVKVLBVOVMLQVORZVLRVMRLQKVKUDSZVKUEZUFVKVPTLBUGMOZPOVNVKHIVONOZBNOZBVO
        VTVKWAUHVKBVOTLVTQJVQVTRZVRVSTLLUIZUJVKWDUKSZULVKHUMZIUMZVOUNOZMWFWGVTU
        NOZMVGWFWAKWGWAKZUQVKWHWIWFWGVKBVOTLVTQJVQWCVRVSWEUOUPURVKBVOLQJVQVRVSU
        SVKBVOTLVTQJVQWCVRVSWEUTWBRVKWFWGVOVAOZMWFWGVTVAOZMVGWFWBKWJUQVKWKWLWFW
        GVKBVOTLVTQJVQWCVRVSWEVBUPURVKBVOLQVQVRVSVCVDVKCVTPBCJFVEVFVHVIVJ $.
        $( [9-Feb-2015] $)

      $( The value of the set of univariate polynomials. $)
      ply1subrg $p |- ( R e. Ring -> U e. ( SubRing ` S ) ) $=
        ( vx vy crg wcel c1o co cbs cfv csubrg con0 eqid a1i c0 cmpl ply1bas id
        cmps 1on mplsubrg copws eqidd cxp wss 0ss opsrbas cplusg wceq opsrplusg
        cv oveqd adantr cmulr opsrmulr subrgpropd psr1val fveq2d eqtr4d eleqtrd
        wa eqeltrd ) BJKZDLBUAMZNOZCPOZABCDJEFGUBVHVJLBUDMZPOZVKVHVIBVLVJLQVLRZ
        VIRVJRLQKVHUESZVHUCZUFVHVMTLBUGMOZPOVKVHHIVLNOZVLVQVHVRUHVHBVLTLVQQJVNV
        QRZVOVPTLLUIZUJVHVTUKSZULVHHUPZIUPZVLUMOZMWBWCVQUMOZMUNWBVRKWCVRKVFZVHW
        DWEWBWCVHBVLTLVQQJVNVSVOVPWAUOUQURVHWBWCVLUSOZMWBWCVQUSOZMUNWFVHWGWHWBW
        CVHBVLTLVQQJVNVSVOVPWAUTUQURVAVHCVQPBCJFVBVCVDVEVG $.
        $( [9-Feb-2015] $)
    $}

    $( The ring of univariate polynomials is a commutative ring. $)
    ply1crng $p |- ( R e. CRing -> P e. CRing ) $=
      ( ccrg wcel cps1 cfv cbs cress co cmpl eqid ply1val ply1bas oveq2d eqtr4d
      c1o csubrg psr1crng crg crngrng ply1subrg syl subrgcrng syl2anc eqeltrd )
      BDEZABFGZAHGZIJZDUGAUHQBKJHGZIJUJABUHDCUHLZMUGUIUKUHIABUHUIDCULUILZNOPUGU
      HDEUIUHRGEZUJDEBUHULSUGBTEUNBUAABUHUICULUMUBUCUIUHUJUJLUDUEUF $.
      $( [9-Feb-2015] $)

    $( The ring of univariate polynomials is an associative algebra. $)
    ply1assa $p |- ( R e. CRing -> P e. AssAlg ) $=
      ( ccrg wcel cps1 cfv cbs cress co casa cmpl ply1val ply1bas oveq2d eqtr4d
      c1o eqid csubrg syl clss crg crngrng ply1subrg ply1lss cur wa wb psr1assa
      wss subrg1cl subrgss issubassa syl3anc mpbir2and eqeltrd ) BDEZABFGZAHGZI
      JZKUQAURQBLJHGZIJUTABURDCURRZMUQUSVAURIABURUSDCVBUSRZNOPUQUTKEZUSURSGEZUS
      URUAGZEZUQBUBEZVEBUCZABURUSCVBVCUDTZUQVHVGVIABURUSCVBVCUETUQURKEURUFGZUSE
      ZUSURHGZUJZVDVEVGUGUHBURVBUIUQVEVLVJUSURVKVKRZUKTUQVEVNVJUSVMURVMRZULTUSU
      TVFVKVMURUTRVFRVPVOUMUNUOUP $.
      $( [9-Feb-2015] $)
  $}

