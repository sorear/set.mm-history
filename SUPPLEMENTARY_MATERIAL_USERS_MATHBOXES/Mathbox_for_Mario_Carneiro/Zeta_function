$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Zeta function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c zeta $. $( Riemann zeta function $)

  $( The Riemann zeta function. $)
  czeta $a class zeta $.

  ${
    $d f k n s $.
    $( Define the Riemann zeta function.  This definition uses a series
       expansion of the alternating zeta function ~ zetaalt that is convergent
       everywhere except ` 1 ` , but going from the alternating zeta function
       to the regular zeta function requires dividing by
       ` 1 - 2 ^ ( 1 - s ) ` , which has zeroes other than ` 1 ` .  To extract
       the correct value of the zeta function at these points, we extend the
       divided alternating zeta function by continuity. $)
    df-zeta $a |- zeta = ( iota_ f e. ( ( CC \ { 1 } ) -cn-> CC )
      A. s e. ( CC \ { 1 } ) ( ( 1 - ( 2 ^c ( 1 - s ) ) ) x. ( f ` s ) ) =
      sum_ n e. NN0 ( sum_ k e. ( 0 ... n ) ( ( ( -u 1 ^ k ) x. ( n _C k ) ) x.
        ( ( k + 1 ) ^c s ) ) / ( 2 ^ ( n + 1 ) ) ) ) $.
  $}

  ${
    $d k m n S $.  $d k F $.  $d k m ph $.
    zetacvg.1 $e |- ( ph -> S e. CC ) $.
    zetacvg.2 $e |- ( ph -> 1 < ( Re ` S ) ) $.
    zetacvg.3 $e |- ( ( ph /\ k e. NN ) -> ( F ` k ) = ( k ^c -u S ) ) $.
    $( The zeta series is convergent.  (Contributed by Mario Carneiro,
       18-Jul-2014.) $)
    zetacvg $p |- ( ph -> seq 1 ( + , F ) e. dom ~~> ) $=
      ( cfv ccxp co c1 wcel wceq cmul cc cc0 syl cr c2 wbr vn vm cn cv cre cneg
      cmpt nnuz cz 1z a1i wa cabs oveq1 eqid ovex fvmpt adantl clog ce wne nncn
      nnne0 negcl adantr cxpef syl3anc eqtrd fveq2d crp nnrp recnd mulcl syl2an
      relogcl absef cim cmin remul reneg rere oveqan12d reim0 oveq2d imcl mul01
      3syl sylan9eqr oveq12d renegcl subid1 3eqtrd eqtr4d cxpcl syl2anc eqeltrd
      recl caddc cseq cli cdm cn0 cexp cdiv 2re 2pos elrpii 1re resubcl sylancr
      rpcxpcl rpcn clt addid2 breqtrrd 0re ltsubadd mp3an13 mpbird 1lt2 mpanl12
      wb cxplt sylancl mpbid cle rprege0 absid 2cn cxp0 3brtr4d reeflog remulcl
      efle mulneg1 cxpmul cxpexp ax-1cn 2nn 3eqtr3d ax-mp eqcomi geolim syl2anr
      oveq2 seqex breldm rpre rpge0 nnre lep1 lt01 lttrd lemul2 syl112anc leneg
      nn0re mulcom simpr negsub eqcomd nnexpcl mpan cxpadd syl211anc sylan cxp1
      peano2nn oveq1d 3eqtr4d climcnds abscvgcvg ) ACUAUCUAUDZBUEHZUFZIJZUGZDKU
      CUHKUILAUJUKACUDZUCLZULZUVRUVQHZUVRUVOIJZUVRDHZUMHZUVSUWAUWBMAUAUVRUVPUWB
      UCUVQUVMUVRUVOIUNUVQUOZUVRUVOIUPUQURZUVTUWDBUFZUVRUSHZNJZUTHZUMHZUWIUEHZU
      THZUWBUVTUWCUWJUMUVTUWCUVRUWGIJZUWJGUVTUVROLZUVRPVAZUWGOLZUWNUWJMUVSUWOAU
      VRVBURZUVSUWPAUVRVCURZAUWQUVSABOLZUWQEBVDQZVEZUVRUWGVFVGVHVIUVTUWIOLZUWKU
      WMMAUWQUWHOLZUXCUVSUXAUVSUWHUVSUVRVJLZUWHRLZUVRVKZUVRVOQZVLZUWGUWHVMVNUWI
      VPQUVTUWMUVOUWHNJZUTHZUWBUVTUWLUXJUTUVTUWLUWGUEHZUWHUEHZNJZUWGVQHZUWHVQHZ
      NJZVRJZUXJPVRJZUXJAUWQUXDUWLUXRMUVSUXAUXIUWGUWHVSVNUVTUXNUXJUXQPVRAUVSUXL
      UVOUXMUWHNAUWTUXLUVOMEBVTQUVSUXFUXMUWHMUXHUWHWAQWBUVSAUXQUXOPNJZPUVSUXPPU
      XONUVSUXFUXPPMUXHUWHWCQWDAUWQUXOOLUXTPMUXAUWQUXOUWGWEVLUXOWFWGWHWIUVTUXJO
      LZUXSUXJMAUVOOLZUXDUYAUVSAUVOAUWTUVNRLZUVORLZEBWQZUVNWJWGZVLZUXIUVOUWHVMV
      NUXJWKQWLVIUVTUWOUWPUYBUWBUXKMUWRUWSAUYBUVSUYGVEZUVRUVOVFVGZWMWLWMUVTUWCU
      WNOGUVTUWOUWQUWNOLUWRUXBUVRUWGWNWOWPAWRUVQKWSWTXAZLWRUAXBSKUVNVRJZIJZUVMX
      CJZUGZPWSZUYJLZAUYOKKUYLVRJXDJZWTTUYPAUYLUBUYNAUYLVJLZUYLOLZASVJLZUYKRLZU
      YRSXEXFXGZAKRLZUYCVUAXHAUWTUYCEUYEQZKUVNXIXJZSUYKXKXJZUYLXLQZAUYLSPIJZUYL
      UMHZKXMAUYKPXMTZUYLVUHXMTZAVUJKPUVNWRJZXMTZAKUVNVULXMFAUVNOLZVULUVNMAUWTV
      UNEUWTUVNUYEVLQZUVNXNQXOAUYCVUJVUMYBZVUDVUCUYCPRLZVUPXHXPKUVNPXQXRQXSAVUA
      VUQVUJVUKYBZVUEXPSRLKSXMTVUAVUQULVURXEXTSUYKPYCYAYDYEAUYRUYLRLPUYLYFTULVU
      IUYLMVUFUYLYGUYLYHWGKVUHMAVUHKSOLZVUHKMYISYJUUAUUBUKYKUBUDZXBLZVUTUYNHZUY
      LVUTXCJZMAUAVUTUYMVVCXBUYNUVMVUTUYLXCUUEUYNUOUYLVUTXCUPUQURZUUCUYOUYQWTWR
      UYNPUUFUUGQACUBUVQUYNUVTUWAUWBRUWFUVTUWBVJLZUWBRLUVSUXEUYDVVEAUXGUYFUVRUV
      OXKUUDZUWBUUHQWPUVTPUWBUWAYFUVTVVEPUWBYFTVVFUWBUUIQUWFXOUVTUVOUVRKWRJZUSH
      ZNJZUTHZUXKVVGUVQHZUWAYFUVTVVIUXJYFTZVVJUXKYFTZUVTUVNVVHNJZUFZUVNUWHNJZUF
      ZVVIUXJYFUVTVVPVVNYFTZVVOVVQYFTZUVTUWHVVHYFTZVVRUVSVVTAUVSVVTUWHUTHZVVHUT
      HZYFTZUVSUVRVVGVWAVWBYFUVSUVRRLUVRVVGYFTUVRUUJUVRUUKQUVSUXEVWAUVRMUXGUVRY
      LQUVSVVGVJLZVWBVVGMUVSVVGUCLZVWDUVRUVHZVVGVKZQVVGYLQYKUVSUXFVVHRLZVVTVWCY
      BUXHUVSVWEVWDVWHVWFVWGVVGVOZWGZUWHVVHYNWOXSURUVTUXFVWHUYCPUVNXMTZVVTVVRYB
      UVSUXFAUXHURUVTVWEVWDVWHUVSVWEAVWFURZVWGVWIWGAUYCUVSVUDVEAVWKUVSAPKUVNVUQ
      AXPUKVUCAXHUKVUDPKXMTAUULUKFUUMVEUWHVVHUVNUUNUUOYEUVTVVPRLZVVNRLZVVRVVSYB
      AUYCUXFVWMUVSVUDUXHUVNUWHYMVNAUYCVWHVWNUVSVUDVWJUVNVVHYMVNVVPVVNUUPWOYEAV
      UNVVHOLVVIVVOMUVSVUOUVSVVHVWJVLUVNVVHYOVNAVUNUXDUXJVVQMUVSVUOUXIUVNUWHYOV
      NYKUVTVVIRLZUXJRLZVVLVVMYBAUYDVWHVWOUVSUYFVWJUVOVVHYMVNAUYDUXFVWPUVSUYFUX
      HUVOUWHYMVNVVIUXJYNWOYEUVTVVKVVGUVOIJZVVJUVTVWEVVKVWQMVWLUAVVGUVPVWQUCUVQ
      UVMVVGUVOIUNUWEVVGUVOIUPUQQUVTVVGOLZVVGPVAZUYBVWQVVJMUVTVWEVWRVWLVVGVBQUV
      TVWEVWSVWLVVGVCQUYHVVGUVOVFVGVHUVTUWAUWBUXKUWFUYIVHYKAVVAULZVVCSVUTXCJZVX
      AUVOIJZNJZVVBVXAVXAUVQHZNJVWTUYLVUTIJZVXAKIJZVXBNJZVVCVXCVWTSUYKVUTNJZIJZ
      VXAKUVOWRJZIJZVXEVXGVWTVXISVUTUYKNJZIJZSVUTIJZUYKIJZVXKVWTVXHVXLSIVWTUYKO
      LZVUTOLZVXHVXLMAVXPVVAAUYKVUEVLVEZVWTVUTVVAVUTRLZAVUTUUQURZVLZUYKVUTUURWO
      WDVWTUYTVXSVXPVXMVXOMUYTVWTVUBUKZVXTVXRSVUTUYKYPVGVWTVXNVXAUYKVXJIVWTVUSV
      VAVXNVXAMYIAVVAUUSZSVUTYQXJVWTVXJUYKVWTKOLZVUNVXJUYKMYRAVUNVVAVUOVEKUVNUU
      TXJUVAWIWLVWTUYTVUAVXQVXIVXEMVYBAVUAVVAVUEVEVYASUYKVUTYPVGVWTVXAOLZVXAPVA
      ZVYDUYBVXKVXGMVWTVXAUCLZVYEVVAVYGASUCLZVVAVYGYSSVUTUVBZUVCURZVXAVBQZVWTVY
      GVYFVYJVXAVCQVYDVWTYRUKAUYBVVAUYGVEVXAKUVOUVDUVEYTAUYSVVAVXEVVCMVUGUYLVUT
      YQUVFVWTVXFVXAVXBNVWTVYEVXFVXAMVYKVXAUVGQUVIYTVVDVWTVXDVXBVXANVWTVYGVXDVX
      BMVWTVYHVVAVYGYSVYCVYIXJUAVXAUVPVXBUCUVQUVMVXAUVOIUNUWEVXAUVOIUPUQQWDUVJU
      VKXSUVL $.
      $( [18-Jul-2014] $)
  $}

  $(
  @{
    @d k n s S @.
    @( The value of the zeta function. @)
    zetacl @p |- ( ( S e. CC /\ S =/= 1 ) -> ( zeta ` S ) e. CC ) @=
      ? @.
  @}

  @( Give an alternating sequence related to the zeta function. @)
  zetaalt @p |- ( ( S e. CC /\ 0 < ( Re ` S ) ) ->
    ( ( 1 - ( exp ` ( ( 1 - S ) x. ( log ` 2 ) ) ) ) x. ( zeta ` S ) ) =
    sum_ n e. NN ( ( -u 1 ^ n ) x. ( n ^c -u S ) ) ) @=
    ? @.

  @( Derive the standard expression for the zeta function. (This is usually
     quoted as the definition of the zeta function, but it is not convergent
     everywhere, unlike our definition.) @)
  zetaval2 @p |- ( ( S e. CC /\ 1 < ( Re ` S ) ) ->
    ( zeta ` S ) = sum_ n e. NN ( n ^c -u S ) ) @=
    ? @.

  @{
    @( The laurent expansion of the zeta function about the pole at ` s = 1 ` .
       The function ` J ( n ) ` is commonly known as the ` n ` -th Stieltjes
       constant. @)
    zetaval3.1 @e |- J = ( ~~> ` ( m e. NN |->
          ( sum_ k e. ( 1 ... m ) ( ( ( log ` k ) ^ n ) / k ) -
            ( ( ( log ` m ) ^ ( n + 1 ) ) / ( n + 1 ) ) ) ) ) @.
    zetaval3 @p |- ( ( S e. CC /\ S =/= 1 ) -> ( zeta ` S ) =
      ( ( 1 / ( S - 1 ) ) + sum_ n e. NN0
        ( ( ( ( 1 - S ) ^ n ) / ( ! ` n ) ) x. J ) ) ) @=
      ? @.
  @}

  @( The value of the zeta function on the natural numbers. @)
  zetann @p |- ( S e. ( ZZ>= ` 2 ) ->
    ( zeta ` S ) = sum_ n e. NN ( n ^ -u S ) ) @=
    ? @.

  @( The value of ` zeta ( 0 ) ` . @)
  zeta0 @p |- ( zeta ` 0 ) = -u ( 1 / 2 ) @=
    ? @.

  @( The value of ` zeta ( 2 ) ` , whose derivation (with an imprecise proof)
     is attributed to Euler. @)
  zeta2 @p |- ( zeta ` 2 ) = ( ( pi ^ 2 ) / 6 ) @=
    ? @.

  @( Apery's constant, ` zeta ( 3 ) ` , is irrational. @)
  zeta3irr @p |- ( zeta ` 3 ) e/ QQ @=
    ? @.

  @( The trivial zeroes of the zeta function. @)
  zetatrivz @p |- ( N e. NN -> ( zeta ` ( -u 2 x. N ) ) = 0 ) @=
    ? @.

  @( The nontrivial zeroes of the zeta function are in the critical strip.
     The Riemann Hypothesis would replace this conclusion with
     ` ( Re `` S ) = ( 1 / 2 ) ` . @)
  zetazcs @p |- ( ( S e. ( CC \ ZZ ) /\ ( zeta ` S ) = 0 ) ->
      ( Re ` S ) e. ( 0 (,) 1 ) ) @=
    ? @.
  $)

