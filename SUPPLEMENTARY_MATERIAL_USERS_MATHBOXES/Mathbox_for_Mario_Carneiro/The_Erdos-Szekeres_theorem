$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      The Erd&#337;s-Szekeres theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d y F $.  $d y O $.  $d x S $.  $d y X $.
    erdszelem1.1 $e |- S = { y e. ~P ( 1 ... A ) | ( ( F |` y )
        Isom < , O ( y , ( F " y ) ) /\ A e. y ) } $.
    $( Lemma for ~ erdsze . $)
    erdszelem1 $p |- ( X e. S <-> ( X C_ ( 1 ... A )
        /\ ( F |` X ) Isom < , O ( X , ( F " X ) ) /\ A e. X ) ) $=
      ( c1 cfz co cpw wcel cima clt cres wiso wa wceq wb syl wss w3a ovex elpw2
      anbi1i cv reseq2 isoeq1 isoeq4 imaeq2 isoeq5 3bitrd anbi12d elrab2 3anass
      eleq2 3bitr4i ) FHBIJZKZLZFDFMZNEDFOZPZBFLZQZQFURUAZVEQFCLVFVCVDUBUTVFVEF
      URHBIUCUDUEAUFZDVGMZNEDVGOZPZBVGLZQVEAFUSCVGFRZVJVCVKVDVLVJVGVHNEVBPZFVHN
      EVBPZVCVLVIVBRVJVMSVGFDUGVGVHNEVBVIUHTVGVHFNEVBUIVLVHVARVNVCSVGFDUJFVHVAN
      EVBUKTULVGFBUPUMGUNVFVCVDUOUQ $.
      $( [22-Jan-2015] $)

    $( Lemma for ~ erdsze . $)
    erdszelem2 $p |- ( ( # " S ) e. Fin /\ ( # " S ) C_ NN ) $=
      ( vx chash cima cfn wcel cn wss cres c1 cv clt wiso mp2an cvv wfo cfz cpw
      co fzfi pwfi mpbi wa crab ssrab2 eqsstri ssfi wfun cdm cn0 cpnf csn hashf
      cun wf ffun ax-mp ssv fdmi sseqtr4i fores fofi cfv wral w3a erdszelem1 c0
      wne ne0i 3ad2ant3 simp1 sylancr hashnncl syl mpbird sylbi funimass4 mpbir
      wb rgen pm3.2i ) HCIZJKZWGLMZCJKZCWGHCNZUAZWHOBUBUDZUCZJKZCWNMWJWMJKZWOOB
      UEZWMUFUGCAPZDWRIQEDWRNRBWRKUHZAWNUIWNFWSAWNUJUKWNCULSHUMZCHUNZMZWLTUOUPU
      QUSZHUTWTURTXCHVAVBZCTXACVCTXCHURVDVEZCHVFSCWGWKVGSWIGPZHVHLKZGCVIZXGGCXF
      CKXFWMMZXFDXFIQEDXFNRZBXFKZVJZXGABCDEXFFVKXLXGXFVLVMZXKXIXMXJXFBVNVOXLXFJ
      KZXGXMWDXLWPXIXNWQXIXJXKVPWMXFULVQXFVRVSVTWAWEWTXBWIXHWDXDXEGCLHWBSWCWF
      $.
      $( [22-Jan-2015] $)
  $}

  ${
    $d f w x y z B $.  $d f m n s w x y z F $.  $d n s x y I $.  $d f s z K $.
    $d f s w x y z A $.  $d n s x y J $.  $d f s w x y z O $.  $d m s x y R $.
    $d a b m n s w x y z N $.  $d a b f m n s w x y z ph $.  $d m s x y S $.
    $d a b m s w z T $.
    erdsze.n $e |- ( ph -> N e. NN ) $.
    erdsze.f $e |- ( ph -> F : ( 1 ... N ) -1-1-> RR ) $.
    ${
      erdszelem.k $e |- K = ( x e. ( 1 ... N ) |->
        sup ( ( # " { y e. ~P ( 1 ... x ) | ( ( F |` y )
          Isom < , O ( y , ( F " y ) ) /\ x e. y ) } ) , RR , < ) ) $.
      $( Lemma for ~ erdsze . $)
      erdszelem3 $p |- ( A e. ( 1 ... N ) -> ( K ` A ) =
        sup ( ( # " { y e. ~P ( 1 ... A ) | ( ( F |` y )
          Isom < , O ( y , ( F " y ) ) /\ A e. y ) } ) , RR , < ) ) $=
        ( chash cv cima clt wcel c1 cfz co cr cres wiso wa crab csup wceq oveq2
        cpw pweqd eleq1 anbi2d rabeqbidv imaeq2d supeq1d ltso supex fvmpt ) BDL
        CMZEURNOHEURUAUBZBMZURPZUCZCQUTRSZUHZUDZNZTOUELUSDURPZUCZCQDRSZUHZUDZNZ
        TOUEQGRSFUTDUFZTVFVLOVMVEVKLVMVBVHCVDVJVMVCVIUTDQRUGUIVMVAVGUSUTDURUJUK
        ULUMUNKTVLOUOUPUQ $.
        $( [22-Jan-2015] $)

      erdszelem.o $e |- O Or RR $.
      $( Lemma for ~ erdsze . $)
      erdszelem4 $p |- ( ( ph /\ A e. ( 1 ... N ) ) -> { A } e.
        { y e. ~P ( 1 ... A ) | ( ( F |` y )
          Isom < , O ( y , ( F " y ) ) /\ A e. y ) } ) $=
        ( c1 wcel wa wss clt adantl wbr cr cfz co csn cima cres wiso cv crab cn
        cpw elfznn elfz1end sylib snssd cfv wi wb elsni breqan12d wn cuz fzssuz
        wral cz uzssz zssre sstri simpr adantr sseldi pm2.21d sylbid ralrimivva
        ltnr syl wf wf1 f1f wor ltso soss soisores mpanl12 syl2anc mpbird snidg
        mp2 eqid erdszelem1 syl3anbrc ) ADMGUAUBZNZOZDUCZMDUAUBZPWNEWNUDQHEWNUE
        UFZDWNNZWNCUGZEWRUDQHEWRUEUFDWRNOCWOUJUHZNWMDWOWMDUINZDWONWLWTADGUKRDUL
        UMUNWMWPBUGZWRQSZXAEUOWREUOHSZUPZCWNVCBWNVCZWMXDBCWNWNWMXAWNNZWRWNNZOZO
        ZXBDDQSZXCXHXBXJUQWMXFXGXADWRDQXADURWRDURUSRXIXJXCXIDTNXJUTXIWKTDWKMVAU
        OZTMGVBXKVDTMVEVFVGVGZWMWLXHAWLVHZVIVJDVNVOVKVLVMWMWKTEVPZWNWKPZWPXEUQZ
        AXNWLAWKTEVQXNJWKTEVRVOVIWMDWKXMUNWKQVSZTHVSXNXOOXPWKTPTQVSXQXLVTWKTQWA
        WGLBCWNWKTQHEWBWCWDWEWLWQADWKWFRCDWSEHWNWSWHWIWJ $.
        $( [22-Jan-2015] $)

      $( Lemma for ~ erdsze . $)
      erdszelem5 $p |- ( ( ph /\ A e. ( 1 ... N ) ) -> ( K ` A ) e. ( # "
        { y e. ~P ( 1 ... A ) | ( ( F |` y )
          Isom < , O ( y , ( F " y ) ) /\ A e. y ) } ) ) $=
        ( c1 cfz co wcel chash clt cr c0 wa cfv cv cima cres wiso cpw crab csup
        wceq erdszelem3 adantl wne cdm cin csn cvv snex cn0 cpnf cun hashf fdmi
        eleqtrri erdszelem4 inelcm sylancr imadisj necon3bii sylibr cfn cn eqid
        wss erdszelem2 simpli simpri nnssre sstri wor ltso fisup2g mpan mp3an13
        w3a syl eqeltrd ) ADMGNOPZUAZDFUBZQCUCZEWKUDRHEWKUEUFDWKPUACMDNOUGUHZUD
        ZSRUIZWMWHWJWNUJAABCDEFGHIJKUKULWIWMTUMZWNWMPZWIQUNZWLUOZTUMZWOWIDUPZWQ
        PWTWLPWSWTUQWQDURUQUSUTUPVAQVBVCVDABCDEFGHIJKLVEWTWQWLVFVGWMTWRTQWLVHVI
        VJWMVKPZWOWMSVNZWPXAWMVLVNZCDWLEHWLVMVOZVPWMVLSXAXCXDVQVRVSSRVTXAWOXBWE
        WPWASWMRWBWCWDWFWG $.
        $( [22-Jan-2015] $)

      $( Lemma for ~ erdsze . $)
      erdszelem6 $p |- ( ph -> K : ( 1 ... N ) --> NN ) $=
        ( c1 cfz co cv cima clt wcel wa cn vz chash cres wiso cpw crab csup cvv
        cr ltso supex a1i cmpt wceq cfv cfn erdszelem2 simpri erdszelem5 sseldi
        wss eqid fmpt2d ) ABUALFMNZUBCOZDVEPQGDVEUCUDZBOZVERSCLVGMNUEUFPZUIQUGZ
        TEUHVIUHRAVGVDRSUIVHQUJUKULEBVDVIUMUNAJULAUAOZVDRSUBVFVJVERSCLVJMNUEUFZ
        PZTVJEUOVLUPRVLTVACVJVKDGVKVBUQURABCVJDEFGHIJKUSUTVC $.
        $( [22-Jan-2015] $)

      erdszelem.a $e |- ( ph -> A e. ( 1 ... N ) ) $.
      ${
        erdszelem7.r $e |- ( ph -> R e. NN ) $.
        erdszelem7.m $e |- ( ph -> -. ( K ` A ) e. ( 1 ... ( R - 1 ) ) ) $.
        $( Lemma for ~ erdsze . $)
        erdszelem7 $p |- ( ph -> E. s e. ~P ( 1 ... N )
          ( R <_ ( # ` s ) /\ ( F |` s ) Isom < , O ( s , ( F " s ) ) ) ) $=
          ( wcel c1 cn cv chash cfv wceq cima clt cres wiso wa cfz co crab wrex
          cpw cle wbr wfun cvv cn0 cpnf csn cun wf hashf ax-mp erdszelem5 mpdan
          ffun fvelima sylancr wss w3a wi eqid erdszelem1 simprl1 elfzuz3 fzss2
          cuz syl2anc syl adantr sstrd vex elpw sylibr wn cmin cz wb erdszelem6
          ffvelrn nnuz syl6eleq nnz peano2zm 3syl elfz5 nnltlem1 bitr4d cr nnre
          mtbid cfn erdszelem2 simpri nnssre sstri sseldi lenlt mpbird breqtrrd
          simprr simprl2 jca32 expr sylan2b expimpd reximdv2 mpd ) AJUAZUBUCZDG
          UCZUDZJCUAZFYEUEUFIFYEUGUHDYERUICSDUJUKZUNULZUMZEYBUOUPZYAFYAUEUFIFYA
          UGUHZUIZJSHUJUKZUNZUMAUBUQZYCUBYGUEZRZYHURUSUTVAVBZUBVCYNVDURYQUBVHVE
          ADYLRZYPOABCDFGHIKLMNVFVGZJYCYGUBVIVJAYDYKJYGYMAYAYGRZYDYAYMRZYKUIZYT
          AYAYFVKZYJDYARZVLZYDUUBVMCDYGFIYAYGVNZVOAUUEYDUUBAUUEYDUIZUIZUUAYIYJU
          UHYAYLVKUUAUUHYAYFYLUUCYJUUDYDAVPAYFYLVKZUUGAHDVSUCRZUUIAHTRYRUUJKOTD
          SHVQVTDSHVRWAWBWCYAYLJWDWEWFUUHEYCYBUOAEYCUOUPZUUGAUUKYCEUFUPZWGZAYCS
          ESWHUKZUJUKRZUULQAUUOYCUUNUOUPZUULAYCSVSUCZRUUNWIRZUUOUUPWJAYCTUUQAYL
          TGVCYRYCTRZABCFGHIKLMNWKOYLTDGWLVTZWMWNAETRZEWIRUURPEWOEWPWQYCSUUNWRV
          TAUUSUVAUULUUPWJUUTPYCEWSVTWTXCAEXARZYCXARUUKUUMWJAUVAUVBPEXBWAAYOXAY
          CYOTXAYOXDRYOTVKCDYGFIUUFXEXFXGXHYSXIEYCXJVTXKWBAUUEYDXMXLUUCYJUUDYDA
          XNXOXPXQXRXSXT $.
          $( [22-Jan-2015] $)
      $}

      erdszelem.b $e |- ( ph -> B e. ( 1 ... N ) ) $.
      erdszelem.l $e |- ( ph -> A < B ) $.
      $( Lemma for ~ erdsze . $)
      erdszelem8 $p |- ( ph ->
        ( ( K ` A ) = ( K ` B ) -> -. ( F ` A ) O ( F ` B ) ) ) $=
        ( wbr wcel c1 cr vf vw vz cfv cv chash wceq cima clt cres wa cfz co cpw
        wiso crab wrex wne wi wfun cvv cn0 cpnf csn hashf ffun ax-mp erdszelem5
        cun wf mpdan fvelima sylancr wss w3a eqid erdszelem1 fzfid simplr1 ssfi
        cfn syl2anc hashcl syl nn0re cn nnssre erdszelem6 ad2antrr sseldi caddc
        ffvelrn cle csup c0 wral erdszelem2 simpri sstri wn wb elfznn nnre 3syl
        a1i ltnle mpbid elfzle2 nsyl sseld mtod hashunsng mp2and cuz cz elfzelz
        ltle mpd syl3anbrc fzss2 sstrd snssd unssd wor mpanl12 sselda ad3antrrr
        soisores lenlt adantr fvres adantl syl3anc elsni imbi2d ralrimiv ralunb
        syl5ibrcom ralrimiva mpbird eluz2 elfz1end sylib simplr2 wf1 f1f fzssuz
        elfzuz3 uzssz zssre ltso soss mp2 r19.21bi simpr isorel breqan12d bitrd
        simplr3 syl12anc mtbid simplr sotr2 mpan a1d fveq2d breq2d sylanbrc zre
        syldan pm2.21d breq1d imbi1d ralbidv jca sylibr ssun2 snssg mpbiri snex
        cdm vex unex fdmi eleqtrri funfvima mp2an eqeltrrd ne0i simpli fimaxre2
        sylancl suprub syl31anc erdszelem3 breqtrrd nnnn0 nn0ltp1le ltne necomd
        idi ex neeq1 syl5ibcom sylan2b rexlimdva necon2bd ) ADFUDZEFUDZIQZDGUDZ
        EGUDZAUAUEZUFUDZUXKUGZUACUEZFUXPUHUIIFUXPUJUOZDUXPRUKCSDULUMZUNUPZUQZUX
        JUXKUXLURZUSZAUFUTZUXKUFUXSUHRZUXTVAVBVCVDVIZUFVJUYCVEVAUYEUFVFVGZADSHU
        LUMZRZUYDNABCDFGHIJKLMVHVKUAUXKUXSUFVLVMAUXOUYBUAUXSUXMUXSRAUXMUXRVNZUX
        MFUXMUHZUIIFUXMUJZUOZDUXMRZVOZUXOUYBUSCDUXSFIUXMUXSVPVQAUYNUKZUXJUXNUXL
        URZUSUXOUYBUYOUXJUYPUYOUXJUKZUXLUXNUYQUXNTRZUXLTRUXNUXLUIQZUXLUXNURUYQU
        XNVBRZUYRUYQUXMWARZUYTUYQUXRWARUYIVUAUYQSDVRUYIUYLUYMAUXJVSZUXRUXMVTWBZ
        UXMWCWDZUXNWEWDUYQWFTUXLWGAUXLWFRZUYNUXJAUYGWFGVJEUYGRZVUEABCFGHIJKLMWH
        OUYGWFEGWLWBWIZWJUYQUYSUSUYQUYSUXNSWKUMZUXLWMQZUYQVUHUFUXQEUXPRUKCSEULU
        MZUNUPZUHZTUIWNZUXLWMUYQVULTVNZVULWOURZUBUEZUCUEZWMQUBVULWPUCTUQZVUHVUL
        RZVUHVUMWMQVUNUYQVULWFTVULWARZVULWFVNZCEVUKFIVUKVPZWQZWRWGWSXEZUYQVUSVU
        OUYQUXMEVDZVIZUFUDZVUHVULUYQVUAEUXMRZWTZVVGVUHUGZVUCUYQVVHEUXRRZAVVKWTU
        YNUXJAEDWMQZVVKADEUIQZVVLWTZPADTRZETRZVVMVVNXAAUYHDWFRZVVONDHXBZDXCZXDZ
        AEWFRZVVPAVUFVWAOEHXBWDZEXCWDZDEXFWBXGESDXHXIWIUYQUXMUXREVUBXJXKUYQVUFV
        UAVVIUKVVJUSAVUFUYNUXJOWIZUXMEUYGXLWDXMUYQVVFVUKRZVVGVULRZUYQVVFVUJVNVV
        FFVVFUHUIIFVVFUJUOZEVVFRZVWEUYQUXMVVEVUJUYQUXMUXRVUJVUBAUXRVUJVNZUYNUXJ
        AEDXNUDZRZVWIADXORZEXORZDEWMQZVWKAUYHVWLNDSHXPWDAVUFVWMOESHXPWDAVVMVWNP
        AVVOVVPVVMVWNUSVVTVWCDEXQWBXRDEUUAXSDSEXTWDWIYAUYQEVUJAEVUJRZUYNUXJAVWA
        VWOVWBEUUBUUCWIZYBYCZUYQVWGVUQVUPUIQZVUQFUDZVUPFUDZIQZUSZUBVVFWPZUCVVFW
        PZUYQVXCUCUXMWPZVXCUCVVEWPZUKVXDUYQVXEVXFUYQVXCUCUXMUYQVUQUXMRZUKZVXBUB
        UXMWPZVXBUBVVEWPVXCUYQVXIUCUXMUYQUYLVXIUCUXMWPZUYIUYLUYMAUXJUUDZUYQUYGT
        FVJZUXMUYGVNZUYLVXJXAZAVXLUYNUXJAUYGTFUUEVXLKUYGTFUUFWDWIZUYQUXMUXRUYGV
        UBAUXRUYGVNZUYNUXJAHVWJRZVXPAHWFRUYHVXQJNWFDSHUUHWBDSHXTWDWIYAZUYGUIYDZ
        TIYDZVXLVXMUKVXNUYGTVNTUIYDVXSUYGSXNUDZTSHUUGVYAXOTSUUIUUJWSWSZUUKUYGTU
        IUULUUMZMUCUBUXMUYGTUIIFYHYEWBXGUUNVXHVXBUBVVEVXHVXBVUPVVERZVWRVWSUXIIQ
        ZUSVXHVYEVWRVXHUXHVWSIQZWTZUXJVYEVXHDVUQUIQZVYFVXHVUQDWMQZVYHWTZVXHVUQU
        XRRVYIUYQUXMUXRVUQVUBYFVUQSDXHWDVXHVUQTRVVOVYIVYJXAVXHUYGTVUQVYBUYQUXMU
        YGVUQVXRYFZWJVXHUYHVVQVVOAUYHUYNUXJVXGNYGZVVRVVSXDVUQDYIWBXGVXHUYLUYMVX
        GVYHVYFXAUYQUYLVXGVXKYJUYQUYMVXGUYIUYLUYMAUXJUUSYJUYQVXGUUOUYLUYMVXGUKZ
        UKVYHDUYKUDZVUQUYKUDZIQZVYFUXMUYJDVUQUIIUYKUUPVYMVYPVYFXAUYLUYMVXGVYNUX
        HVYOVWSIDUXMFYKVUQUXMFYKUUQYLUURUUTUVAUYOUXJVXGUVBVXHVWSTRZUXHTRZUXITRZ
        VYGUXJUKVYEUSZVXHVXLVUQUYGRVYQUYQVXLVXGVXOYJZVYKUYGTVUQFWLWBVXHVXLUYHVY
        RWUAVYLUYGTDFWLWBVXHVXLVUFVYSWUAUYQVUFVXGVWDYJUYGTEFWLWBVXTVYQVYRVYSVOV
        YTMTVWSUXHUXIIUVCUVDYMXMUVEVYDVXAVYEVWRVYDVWTUXIVWSIVYDVUPEFVUPEYNUVFUV
        GYOYRYPVXBUBUXMVVEYQUVHYSUYQVXCUCVVEUYQVXCVUQVVERZEVUPUIQZVXAUSZUBVVFWP
        UYQWUDUBVVFUYQVUPVVFRZUKWUCVXAUYQWUEVUPVUJRZWUCWTZUYQVVFVUJVUPVWQYFUYQW
        UFUKZVUPEWMQZWUGWUFWUIUYQVUPSEXHYLWUHVUPTRZVVPWUIWUGXAWUFWUJUYQWUFVUPXO
        RWUJVUPSEXPVUPUVIWDYLAVVPUYNUXJWUFVWCYGVUPEYIWBXGUVJUVKYSWUBVXBWUDUBVVF
        WUBVWRWUCVXAWUBVUQEVUPUIVUQEYNUVLUVMUVNYRYPUVOVXCUCUXMVVEYQUVPUYQVXLVVF
        UYGVNZVWGVXDXAZVXOUYQUXMVVEUYGVXRUYQEUYGVWDYBYCVXSVXTVXLWUKUKWULVYCMUCU
        BVVFUYGTUIIFYHYEWBYTUYQVWHVVEVVFVNZVVEUXMUVQUYQVWOVWHWUMXAVWPEVVFVUJUVR
        WDUVSCEVUKFIVVFVVBVQXSUYCVVFUFUWAZRVWEVWFUSUYFVVFVAWUNUXMVVEUAUWBEUVTUW
        CVAUYEUFVEUWDUWEVUKVVFUFUWFUWGWDUWHZVULVUHUWIWDUYQVUNVUTVURVVDVUTVVAVVC
        UWJUCUBVULUWKUWLWUOUCUBVULVUHUWMUWNAUXLVUMUGZUYNUXJAVUFWUPOABCEFGHIJKLU
        WOWDWIUWPUYQUYTUXLVBRZUYSVUIXAVUDUYQVUEWUQVUGUXLUWQWDUXNUXLUWRWBYTUXAUX
        NUXLUWSYMUWTUXBUXOUYPUYAUXJUXNUXKUXLUXCYOUXDUXEUXFXRUXG $.
        $( [22-Jan-2015] $)
    $}

    ${
      erdszelem.i $e |- I = ( x e. ( 1 ... N ) |->
        sup ( ( # " { y e. ~P ( 1 ... x ) | ( ( F |` y )
          Isom < , < ( y , ( F " y ) ) /\ x e. y ) } ) , RR , < ) ) $.
      erdszelem.j $e |- J = ( x e. ( 1 ... N ) |->
        sup ( ( # " { y e. ~P ( 1 ... x ) | ( ( F |` y )
          Isom < , `' < ( y , ( F " y ) ) /\ x e. y ) } ) , RR , < ) ) $.
      erdszelem.t $e |- T = ( n e. ( 1 ... N ) |->
        <. ( I ` n ) , ( J ` n ) >. ) $.
      $( Lemma for ~ erdsze . $)
      erdszelem9 $p |- ( ph -> T : ( 1 ... N ) -1-1-> ( NN X. NN ) ) $=
        ( cn cfv wceq wcel wa cr vz vw va vb c1 cfz co cxp wf cv wi wf1 cop clt
        wral ltso erdszelem6 ffvelrn sylan ccnv wor cnvso opelxpi syl2anc fmptd
        mpbi fveq2 eqeqan12d eqeq12 imbi12d eqcom syl6bb wss cz elfzelz zre syl
        ssriv a1i biidd cle wbr w3a simpr1 opeq12d opex fvmpt eqeq12d fvex opth
        simpr2 wne wn wb sseldi simpr3 leltne syl3anc wo adantr f1fveq syl12anc
        syl6bbr necon3bid bitr4d biimpa f1f ad2antrr lttri2 mpbid simpr anim12d
        erdszelem8 ioran brcnv notbii anbi2i bitr4i syl6ibr ex sylbird necon4ad
        mt2d syl5bi sylbid syl6ib wlogle ralrimivva dff13 sylanbrc ) AUEIUFUGZO
        OUHZDUIUAUJZDPZUBUJZDPZQZYMYOQZUKZUBYKUOUAYKUOYKYLDULAEYKEUJZGPZYTHPZUM
        ZYLDAYTYKRZSUUAORZUUBORZUUCYLRAYKOGUIUUDUUEABCFGIUNJKLUPUQYKOYTGURUSAYK
        OHUIUUDUUFABCFHIUNUTZJKMTUNVATUUGVAUPTUNVBVFZUQYKOYTHURUSUUAUUBOOVCVDNV
        EAYSUAUBYKYKAUCUJZDPZUDUJZDPZQZUUIUUKQZUKYSYSUAUBUCUDYKUUIYMQZUUKYOQZSU
        UMYQUUNYRUUOUUPUUJYNUULYPUUIYMDVGUUKYODVGVHUUIYMUUKYOVIVJUUIYOQZUUKYMQZ
        SZUUMYQUUNYRUUSUUMYPYNQYQUUQUURUUJYPUULYNUUIYODVGUUKYMDVGVHYPYNVKVLUUSU
        UNYOYMQZYRUUIYOUUKYMVIYOYMVKZVLVJYKTVMAUAYKTYMYKRZYMVNRYMTRZYMUEIVOYMVP
        VQZVRZVSAUVBYOYKRZSSYSVTAUVBUVFYMYOWAWBZWCZSZYQUUTYRUVIYQYMGPZYMHPZUMZY
        OGPZYOHPZUMZQZUUTUVIYNUVLYPUVOUVIUVBYNUVLQAUVBUVFUVGWDZEYMUUCUVLYKDYTYM
        QUUAUVJUUBUVKYTYMGVGYTYMHVGWENUVJUVKWFWGVQUVIUVFYPUVOQAUVBUVFUVGWKZEYOU
        UCUVOYKDYTYOQUUAUVMUUBUVNYTYOGVGYTYOHVGWENUVMUVNWFWGVQWHUVPUVJUVMQZUVKU
        VNQZSZUVIUUTUVJUVKUVMUVNYMGWIYMHWIYOHWIWJUVIUWAYOYMUVIYOYMWLZYMYOUNWBZU
        WAWMZUVIUVCYOTRUVGUWCUWBWNUVIUVBUVCUVQUVDVQUVIYKTYOUVEUVRWOAUVBUVFUVGWP
        YMYOWQWRZUVIUWCUWDUVIUWCSZUWAYMFPZYOFPZUNWBZUWHUWGUNWBZWSZUWFUWGUWHWLZU
        WKUVIUWCUWLUVIUWCUWBUWLUWEUVIUWGUWHYOYMUVIUWGUWHQZYRUUTUVIYKTFULZUVBUVF
        UWMYRWNAUWNUVHKWTUVQUVRYKTYMYOFXAXBUVAXCXDXEXFUWFUWGTRZUWHTRZUWLUWKWNUW
        FYKTFUIZUVBUWOAUWQUVHUWCAUWNUWQKYKTFXGVQXHZUVIUVBUWCUVQWTZYKTYMFURVDUWF
        UWQUVFUWPUWRUVIUVFUWCUVRWTZYKTYOFURVDUWGUWHXIVDXJUWFUWAUWIWMZUWGUWHUUGW
        BZWMZSZUWKWMZUWFUVSUXAUVTUXCUWFBCYMYOFGIUNAIORUVHUWCJXHZAUWNUVHUWCKXHZL
        UPUWSUWTUVIUWCXKZXMUWFBCYMYOFHIUUGUXFUXGMUUHUWSUWTUXHXMXLUXEUXAUWJWMZSU
        XDUWIUWJXNUXCUXIUXAUXBUWJUWGUWHUNYMFWIYOFWIXOXPXQXRXSYCXTYAYBYDYEUVAYFY
        GYHUAUBYKYLDYIYJ $.
        $( [22-Jan-2015] $)

      erdszelem.r $e |- ( ph -> R e. NN ) $.
      erdszelem.s $e |- ( ph -> S e. NN ) $.
      erdszelem.m $e |- ( ph -> ( ( R - 1 ) x. ( S - 1 ) ) < N ) $.
      $( Lemma for ~ erdsze . $)
      erdszelem10 $p |- ( ph -> E. m e. ( 1 ... N )
        ( -. ( I ` m ) e. ( 1 ... ( R - 1 ) ) \/
          -. ( J ` m ) e. ( 1 ... ( S - 1 ) ) ) ) $=
        ( vs cv cfv c1 cmin co cfz cxp wcel wn wrex wo crn wss csdm wbr cdom wi
        cvv cop cmpt ovex mptex eqeltri rnex ssdomg ax-mp domnsym syl cen chash
        clt cmul cfn wceq fzfid hashxp syl2anc cn0 nnm1nn0 hashfz1 3syl oveq12d
        cn eqtrd nnnn0 3brtr4d wb idi xpfi hashsdom wf1 wf1o erdszelem9 f1f1orn
        mpbid f1oen sdomentr nsyl3 wex nss df-rex bitr4i sylib wfn eleq1 notbid
        wa f1fn rexrn fveq2 opeq12d opex fvmpt adantl eleq1d fvex opelxp syl6bb
        ianor rexbidva ) AGUBZFUCZUDDUDUEUFZUGUFZUDEUDUEUFZUGUFZUHZUIZUJZGUDLUG
        UFZUKZYBJUCZYEUIZUJYBKUCZYGUIZUJULZGYKUKAUAUBZYHUIZUJZUAFUMZUKZYLAUUAYH
        UNZUJZUUBUUCYHUUAUOUPZAUUCUUAYHUQUPZUUEUJUUAUSUIZUUCUUFURFFHYKHUBZJUCZU
        UHKUCZUTZVAUSQHYKUUKUDLUGVBZVCVDVEZUUAYHUSVFVGUUAYHVHVIAYHYKUOUPZYKUUAV
        JUPZUUEAYHVKUCZYKVKUCZVLUPZUUNAYDYFVMUFZLUUPUUQVLTAUUPYEVKUCZYGVKUCZVMU
        FZUUSAYEVNUIZYGVNUIZUUPUVBVOAUDYDVPZAUDYFVPZYEYGVQVRAUUTYDUVAYFVMADWDUI
        YDVSUIUUTYDVORDVTYDWAWBAEWDUIYFVSUIUVAYFVOSEVTYFWAWBWCWEALWDUILVSUIUUQL
        VOMLWFLWAWBWGAYHVNUIZYKVNUIZUURUUNWHAUVCUVDUVGAUVCURUVEWIAUVDURUVFWIYEY
        GWJVRAUVHURAUDLVPWIYHYKWKVRWPAYKWDWDUHZFWLZYKUUAFWMUUOABCFHIJKLMNOPQWNZ
        YKUVIFWOYKUUAFUULWQWBUUGUUNUUOXHUUEURUUMYHYKUUAUSWRVGVRWSUUDYRUUAUIYTXH
        UAWTUUBUAUUAYHXAYTUAUUAXBXCXDAUVJFYKXEUUBYLWHUVKYKUVIFXIYTYJUAGYKFYRYCV
        OYSYIYRYCYHXFXGXJWBWPAYJYQGYKAYBYKUIZXHZYJYNYPXHZUJYQUVMYIUVNUVMYIYMYOU
        TZYHUIUVNUVMYCUVOYHUVLYCUVOVOAHYBUUKUVOYKFUUHYBVOUUIYMUUJYOUUHYBJXKUUHY
        BKXKXLQYMYOXMXNXOXPYMYOYEYGYBKXQXRXSXGYNYPXTXSYAWP $.
        $( [22-Jan-2015] $)

      $( Lemma for ~ erdsze . $)
      erdszelem11 $p |- ( ph -> E. s e. ~P ( 1 ... N )
      ( ( R <_ ( # ` s ) /\ ( F |` s ) Isom < , < ( s , ( F " s ) ) ) \/
        ( S <_ ( # ` s ) /\ ( F |` s ) Isom < , `' < ( s , ( F " s ) ) ) ) ) $=
        ( vm cv chash cfv cle wbr cima clt cres wiso wa c1 cfz co cpw wrex ccnv
        wo cmin wcel wn erdszelem10 cn adantr wf1 ltso simprl simprr erdszelem7
        cr expr wor cnvso mpbi orim12d rexlimdva mpd r19.43 sylibr ) ADLUBZUCUD
        ZUEUFVTHVTUGZUHUHHVTUIZUJUKZLULKUMUNZUOZUPZEWAUEUFVTWBUHUHUQZWCUJUKZLWF
        UPZURZWDWIURLWFUPAUAUBZIUDULDULUSUNUMUNUTVAZWLJUDULEULUSUNUMUNUTVAZURZU
        AWEUPWKABCDEFUAGHIJKMNOPQRSTVBAWOWKUAWEAWLWEUTZUKWMWGWNWJAWPWMWGAWPWMUK
        ZUKBCWLDHIKUHLAKVCUTZWQMVDAWEVJHVEZWQNVDOVFAWPWMVGADVCUTWQRVDAWPWMVHVIV
        KAWPWNWJAWPWNUKZUKBCWLEHJKWHLAWRWTMVDAWSWTNVDPVJUHVLVJWHVLVFVJUHVMVNAWP
        WNVGAEVCUTWTSVDAWPWNVHVIVKVOVPVQWDWILWFVRVS $.
        $( [22-Jan-2015] $)
    $}

    erdsze.r $e |- ( ph -> R e. NN ) $.
    erdsze.s $e |- ( ph -> S e. NN ) $.
    erdsze.l $e |- ( ph -> ( ( R - 1 ) x. ( S - 1 ) ) < N ) $.
    $( The Erd&#337;s-Szekeres theorem.  For any injective sequence ` F ` on
       the reals of length at least ` ( R - 1 ) x. ( S - 1 ) + 1 ` , there is
       either a subsequence of length at least ` R ` on which ` F ` is
       increasing (i.e. a ` < , < ` order isomorphism) or a subsequence of
       length at least ` S ` on which ` F ` is decreasing (i.e. a ` < , ``' < `
       order isomorphism, recalling that ` ``' < ` is the greater-than
       relation).  (Contributed by Mario Carneiro, 22-Jan-2015.) $)
    erdsze $p |- ( ph -> E. s e. ~P ( 1 ... N )
    ( ( R <_ ( # ` s ) /\ ( F |` s ) Isom < , < ( s , ( F " s ) ) ) \/
      ( S <_ ( # ` s ) /\ ( F |` s ) Isom < , `' < ( s , ( F " s ) ) ) ) ) $=
      ( vy vz chash cima clt wiso wa crab cr vx vn vw c1 cfz cres wcel cpw csup
      co cv cmpt cfv ccnv cop wceq wb reseq2 isoeq1 isoeq4 imaeq2 isoeq5 3bitrd
      syl eleq2 anbi12d cbvrabv oveq2 pweqd eleq1 anbi2d syl5eq imaeq2d supeq1d
      rabeqbidv cbvmptv eqid erdszelem11 ) AUALBCUBUDEUEUJZUBUKZMVSNUCUKZDWAOZP
      PDWAUFZQZMUKZWAUGZRZUCUDWEUEUJZUHZSZOZTPUIZULZUMVTMVSNWAWBPPUNZWCQZWFRZUC
      WISZOZTPUIZULZUMUOULZUBDWMWTEFGHMUAVSWLNLUKZDXBOZPPDXBUFZQZUAUKZXBUGZRZLU
      DXFUEUJZUHZSZOZTPUIWEXFUPZTWKXLPXMWJXKNXMWJXEWEXBUGZRZLWISXKWGXOUCLWIWAXB
      UPZWDXEWFXNXPWDWAWBPPXDQZXBWBPPXDQZXEXPWCXDUPZWDXQUQWAXBDURZWAWBPPXDWCUSV
      DWAWBXBPPXDUTXPWBXCUPZXRXEUQWAXBDVAZXBWBXCPPXDVBVDVCWAXBWEVEZVFVGXMXOXHLW
      IXJXMWHXIWEXFUDUEVHVIZXMXNXGXEWEXFXBVJZVKVOVLVMVNVPMUAVSWSNXBXCPWNXDQZXGR
      ZLXJSZOZTPUIXMTWRYIPXMWQYHNXMWQYFXNRZLWISYHWPYJUCLWIXPWOYFWFXNXPWOWAWBPWN
      XDQZXBWBPWNXDQZYFXPXSWOYKUQXTWAWBPWNXDWCUSVDWAWBXBPWNXDUTXPYAYLYFUQYBXBWB
      XCPWNXDVBVDVCYCVFVGXMYJYGLWIXJYDXMXNXGYFYEVKVOVLVMVNVPXAVQIJKVR $.
      $( [22-Jan-2015] $)
  $}

  ${
    $d f s t A $.  $d f s t F $.  $d s t x y G $.  $d f s t R $.  $d f s t S $.
    $d f s t x y N $.  $d f s t x y ph $.
    erdsze2.r $e |- ( ph -> R e. NN ) $.
    erdsze2.s $e |- ( ph -> S e. NN ) $.
    erdsze2.f $e |- ( ph -> F : A -1-1-> RR ) $.
    erdsze2.a $e |- ( ph -> A C_ RR ) $.
    ${
      erdsze2lem.n $e |- N = ( ( R - 1 ) x. ( S - 1 ) ) $.
      erdsze2lem.l $e |- ( ph -> N < ( # ` A ) ) $.
      $( Lemma for ~ erdsze2 . $)
      erdsze2lem1 $p |- ( ph -> E. f ( f : ( 1 ... ( N + 1 ) ) -1-1-> A /\
        f Isom < , < ( ( 1 ... ( N + 1 ) ) , ran f ) ) ) $=
        ( c1 clt wcel cn0 syl syl2anc wb vs caddc co cfz cv cen wbr wss wex wf1
        wa crn wiso cdom cfn chash cfv wceq cmin cmul nnm1nn0 nn0mulcl syl5eqel
        cn peano2nn0 hashfz1 adantr hashcl adantl nn0ltp1le mpbid eqbrtrd fzfid
        cle simpr hashdom wn isinffi cvv cr reex ssexg sylancl brdomg pm2.61dan
        mpbird domeng wor simprr sstrd ltso soss ee10 vex simprl sylancr fz1iso
        enfi wf1o isof1o hashen eqtr3d oveq2d f1of1 simplrr f1ss wfo f1ofo forn
        f1oeq2 3syl isoeq5 isoeq4 jca ex eximdv mpd exlimdv ) ANGNUBUCZUDUCZUAU
        EZUFUGZYABUHZUKZUAUIZXTBEUEZUJZXTYFULZOOYFUMZUKZEUIZAXTBUNUGZYEABUOPZYL
        AYMUKZXTUPUQZBUPUQZVNUGZYLYNYOXSYPVNAYOXSURZYMAXSQPZYRAGQPZYSAGCNUSUCZD
        NUSUCZUTUCZQLAUUAQPZUUBQPZUUCQPACVDPUUDHCVARADVDPUUEIDVARUUAUUBVBSVCZGV
        ERXSVFRZVGYNGYPOUGZXSYPVNUGZAUUHYMMVGYNYTYPQPZUUHUUITAYTYMUUFVGYMUUJABV
        HVIGYPVJSVKVLYNXTUOPZYMYQYLTYNNXSVMAYMVOXTBVPSVKAYMVQZUKZYLYGEUIZUUMUUL
        UUKUUNAUULVOUUMNXSVMBXTEVRSUUMBVSPZYLUUNTAUUOUULABVTUHZVTVSPUUOKWABVTVS
        WBWCZVGXTBVSEWDRWFWEAUUOYLYETUUQUAXTBVSWGRVKAYDYKUAAYDYKAYDUKZNYAUPUQZU
        DUCZYAOOYFUMZEUIZYKUURYAOWHZYAUOPZUVBUURYAVTUHVTOWHUVCUURYABVTAYBYCWIAU
        UPYDKVGWJWKYAVTOWLWMUURUUKUVDUURNXSVMZUURYAVSPYBUUKUVDTUAWNAYBYCWOZXTYA
        VSWRWPVKZYAOEWQSUURUVAYJEUURUVAYJUURUVAUKZYGYIUVHXTYAYFUJZYCYGUVHXTYAYF
        WSZUVIUVHUUTYAYFWSZUVJUVAUVKUURUUTYAOOYFWTVIZUVHUUTXTURZUVKUVJTUVHUUSXS
        NUDUURUUSXSURUVAUURYOUUSXSUURYOUUSURZYBUVFUURUUKUVDUVNYBTUVEUVGXTYAXASW
        FAYRYDUUGVGXBVGXCZUUTXTYAYFXJRVKXTYAYFXDRAYBYCUVAXEXTYABYFXFSUVHUUTYHOO
        YFUMZYIUVHUVPUVAUURUVAVOUVHYHYAURZUVPUVATUVHUVKUUTYAYFXGUVQUVLUUTYAYFXH
        UUTYAYFXIXKUUTYHYAOOYFXLRWFUVHUVMUVPYITUVOUUTYHXTOOYFXMRVKXNXOXPXQXOXRX
        Q $.
        $( [22-Jan-2015] $)

      erdsze2lem.g $e |- ( ph -> G : ( 1 ... ( N + 1 ) ) -1-1-> A ) $.
      erdsze2lem.i $e |- ( ph ->
        G Isom < , < ( ( 1 ... ( N + 1 ) ) , ran G ) ) $.
      $( Lemma for ~ erdsze2 . $)
      erdsze2lem2 $p |- ( ph -> E. s e. ~P A
      ( ( R <_ ( # ` s ) /\ ( F |` s ) Isom < , < ( s , ( F " s ) ) ) \/
        ( S <_ ( # ` s ) /\ ( F |` s ) Isom < , `' < ( s , ( F " s ) ) ) ) ) $=
        ( clt wiso wcel syl vt vx vy cv chash cfv cle ccom cima cres wa ccnv wo
        wbr c1 caddc co cfz cpw wrex cn0 cn cmin cmul nnm1nn0 nn0mulcl syl5eqel
        syl2anc nn0p1nn cr wf1 f1co nn0re ltp1 3syl syl5eqbrr erdsze wss wi vex
        elpw wceq cen f1imaen sylan cfn wb fzfid simpr ssfi enfi sylancr mpbird
        cvv hashen breq2d biimprd wral crn ad2antrr adantr simprl sseldd simprr
        isorel syl12anc biimpd ralrimivva wor wf elfznn nnre a1i ltso soss ee10
        ssriv f1f soisores syl22anc isocnv isotr ex resco coeq1i coass cid wf1o
        eqtri f1ores f1ococnv2 isoeq1 isoeq5 ax-mp syl6bb sylibd anim12d isoeq4
        3bitrd anbi12d coeq2d wrel f1rel coires1 eqtrd syl5eq imaco orim12d frn
        imassrn syl5ss ssexg sylancl elpw2g jctild fveq2 reseq2 orbi12d rcla4ev
        reex imaeq2 syl6 sylan2b rexlimdva mpd ) ACUAUDZUEUFZUGUNZUVFEFUHZUVFUI
        ZQQUVIUVFUJZRZUKZDUVGUGUNZUVFUVJQQULZUVKRZUKZUMZUAUOGUOUPUQZURUQZUSZUTC
        HUDZUEUFZUGUNZUWBEUWBUIZQQEUWBUJZRZUKZDUWCUGUNZUWBUWEQUVOUWFRZUKZUMZHBU
        SZUTZACDUVIUVSUAAGVASZUVSVBSAGCUOVCUQZDUOVCUQZVDUQZVAMAUWPVASZUWQVASZUW
        RVASACVBSUWSICVETADVBSUWTJDVETUWPUWQVFVHVGZGVITABVJEVKZUVTBFVKZUVTVJUVI
        VKKOUVTBVJEFVLVHIJAUWRGUVSQMAUWOGVJSGUVSQUNUXAGVMGVNVOVPVQAUVRUWNUAUWAU
        VFUWASAUVFUVTVRZUVRUWNVSUVFUVTUAVTZWAAUXDUKZUVRFUVFUIZUWMSZCUXGUEUFZUGU
        NZUXGEUXGUIZQQEUXGUJZRZUKZDUXIUGUNZUXGUXKQUVOUXLRZUKZUMZUKUWNUXFUVRUXRU
        XHUXFUVMUXNUVQUXQUXFUVHUXJUVLUXMUXFUXJUVHUXFUXIUVGCUGUXFUXIUVGWBZUXGUVF
        WCUNZAUXCUXDUXTOUVTBUVFFUXEWDWEZUXFUXGWFSZUVFWFSZUXSUXTWGUXFUYBUYCUXFUV
        TWFSUXDUYCUXFUOUVSWHAUXDWIZUVTUVFWJVHZUXFUVFWNSUXTUYBUYCWGUXEUYAUXGUVFW
        NWKWLWMUYEUXGUVFWOVHWMZWPWQUXFUVLUXGUVJQQUVKFUVFUJZULZUHZRZUXMUXFUXGUVF
        QQUYHRZUVLUYJVSUXFUVFUXGQQUYGRZUYKUXFUYLUBUDZUCUDZQUNZUYMFUFUYNFUFQUNZV
        SZUCUVFWRUBUVFWRZUXFUYQUBUCUVFUVFUXFUYMUVFSZUYNUVFSZUKZUKZUYOUYPVUBUVTF
        WSZQQFRZUYMUVTSUYNUVTSUYOUYPWGAVUDUXDVUAPWTVUBUVFUVTUYMUXFUXDVUAUYDXAZU
        XFUYSUYTXBXCVUBUVFUVTUYNVUEUXFUYSUYTXDXCUVTVUCUYMUYNQQFXEXFXGXHUXFUVTQX
        IZBQXIZUVTBFXJZUXDUYLUYRWGUXFUVTVJVRZVJQXIZVUFVUIUXFUAUVTVJUVFUVTSUVFVB
        SUVFVJSUVFUVSXKUVFXLTXQXMXNUVTVJQXOXPUXFBVJVRZVUJVUGAVUKUXDLXAXNBVJQXOX
        PAVUHUXDAUXCVUHOUVTBFXRZTXAUYDUBUCUVFUVTBQQFXSXTWMUVFUXGQQUYGYATZUYKUVL
        UYJUXGUVFUVJQQQUVKUYHYBYCTUXFUYJUXGUVJQQUXLRZUXMUXFUYIUXLWBZUYJVUNWGUXF
        UYIEUYGUYHUHZUHZUXLUYIEUYGUHZUYHUHVUQUVKVURUYHEFUVFYDYEEUYGUYHYFYIUXFVU
        QEYGUXGUJZUHZUXLUXFVUPVUSEUXFUVFUXGUYGYHZVUPVUSWBAUXCUXDVVAOUVTBUVFFYJW
        EUVFUXGUYGYKTUUAUXFEUUBZVUTUXLWBAVVBUXDAUXBVVBKBVJEUUCTXAEUXGUUDTUUEUUF
        ZUXGUVJQQUXLUYIYLTUVJUXKWBZVUNUXMWGEFUVFUUGZUXGUVJUXKQQUXLYMYNYOYPYQUXF
        UVNUXOUVPUXPUXFUXOUVNUXFUXIUVGDUGUYFWPWQUXFUVPUXGUVJQUVOUYIRZUXPUXFUYKU
        VPVVFVSVUMUYKUVPVVFUXGUVFUVJQQUVOUVKUYHYBYCTUXFVVFUXGUVJQUVOUXLRZUXPUXF
        VUOVVFVVGWGVVCUXGUVJQUVOUXLUYIYLTVVDVVGUXPWGVVEUXGUVJUXKQUVOUXLYMYNYOYP
        YQUUHAUXHUXDAUXHUXGBVRZAUXGVUCBFUVFUUJAUXCVUHVUCBVROVULUVTBFUUIVOUUKABW
        NSZUXHVVHWGAVUKVJWNSVVILUUTBVJWNUULUUMUXGBWNUUNTWMXAUUOUWLUXRHUXGUWMUWB
        UXGWBZUWHUXNUWKUXQVVJUWDUXJUWGUXMVVJUWCUXICUGUWBUXGUEUUPZWPVVJUWGUWBUWE
        QQUXLRZUXGUWEQQUXLRZUXMVVJUWFUXLWBZUWGVVLWGUWBUXGEUUQZUWBUWEQQUXLUWFYLT
        UWBUWEUXGQQUXLYRVVJUWEUXKWBZVVMUXMWGUWBUXGEUVAZUXGUWEUXKQQUXLYMTYSYTVVJ
        UWIUXOUWJUXPVVJUWCUXIDUGVVKWPVVJUWJUWBUWEQUVOUXLRZUXGUWEQUVOUXLRZUXPVVJ
        VVNUWJVVRWGVVOUWBUWEQUVOUXLUWFYLTUWBUWEUXGQUVOUXLYRVVJVVPVVSUXPWGVVQUXG
        UWEUXKQUVOUXLYMTYSYTUURUUSUVBUVCUVDUVE $.
        $( [22-Jan-2015] $)
    $}

    erdsze2.l $e |- ( ph -> ( ( R - 1 ) x. ( S - 1 ) ) < ( # ` A ) ) $.
    $( Generalize the statement of the Erd&#337;s-Szekeres theorem ~ erdsze to
       "sequences" indexed by an arbitrary subset of ` RR ` , which can be
       infinite. $)
    erdsze2 $p |- ( ph -> E. s e. ~P A
      ( ( R <_ ( # ` s ) /\ ( F |` s ) Isom < , < ( s , ( F " s ) ) ) \/
        ( S <_ ( # ` s ) /\ ( F |` s ) Isom < , `' < ( s , ( F " s ) ) ) ) ) $=
      ( vf c1 cmin co clt wiso wa wbr adantr caddc cfz cv wf1 crn wex chash cfv
      cmul cle cima cres ccnv wo wrex eqid erdsze2lem1 cn wcel cr simprl simprr
      cpw wss erdsze2lem2 ex exlimdv mpd ) AMCMNODMNOUIOZMUAOUBOZBLUCZUDZVJVKUE
      PPVKQZRZLUFCFUCZUGUHZUJSVOEVOUKZPPEVOULZQRDVPUJSVOVQPPUMVRQRUNFBVCUOZABCD
      LEVIGHIJVIUPZKUQAVNVSLAVNVSAVNRBCDEVKVIFACURUSVNGTADURUSVNHTABUTEUDVNITAB
      UTVDVNJTVTAVIBUGUHPSVNKTAVLVMVAAVLVMVBVEVFVGVH $.
      $( [22-Jan-2015] $)
  $}

