$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Path-connected and simply connected spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c PCon $.
  $c SCon $.

  $( Extend class notation with the class of path-connected topologies. $)
  cpcon $a class PCon $.

  $( Extend class notation with the class of simply connected topologies. $)
  cscon $a class SCon $.

  ${
    $d f j x y $.
    $( Define the class of path-connected topologies.  A topology is
       path-connected if there is a path (a continuous function from the unit
       interval) that goes from ` x ` to ` y ` for any points ` x , y ` in the
       space. $)
    df-pcon $a |- PCon = { j e. Top | A. x e. U. j A. y e. U. j
      E. f e. ( II Cn j ) ( ( f ` 0 ) = x /\ ( f ` 1 ) = y ) } $.

    $( Define the class of simply connected topologies.  A topology is simply
       connected if it is path-connected and every loop (continuous path with
       identical start and endpoint) is contractible to a point (path-homotopic
       to a constant function). $)
    df-scon $a |- SCon = { j e. PCon | A. f e. ( II Cn j ) ( ( f ` 0 ) =
      ( f ` 1 ) -> f ( ~=ph ` j ) ( ( 0 [,] 1 ) X. { ( f ` 0 ) } ) ) } $.
  $}

  ${
    $d f x y A $.  $d f y B $.  $d f j x y J $.  $d j x y X $.
    ispcon.1 $e |- X = U. J $.
    $( The property of being a path-connected topological space.  (Contributed
       by Mario Carneiro, 11-Feb-2015.) $)
    ispcon $p |- ( J e. PCon <-> ( J e. Top /\ A. x e. X A. y e. X
      E. f e. ( II Cn J ) ( ( f ` 0 ) = x /\ ( f ` 1 ) = y ) ) ) $=
      ( vj cc0 cv cfv wceq c1 wa cii ccn co wrex cuni wral raleqbidv ctop cpcon
      unieq syl6eqr oveq2 rexeqdv df-pcon elrab2 ) HCIZJAIKLUIJBIKMZCNGIZOPZQZB
      UKRZSZAUNSUJCNDOPZQZBESZAESGDUAUBUKDKZUOURAUNEUSUNDREUKDUCFUDZUSUMUQBUNEU
      TUSUJCULUPUKDNOUEUFTTABCGUGUH $.
      $( [11-Feb-2015] $)

    $( The property of being a path-connected topological space.  (Contributed
       by Mario Carneiro, 11-Feb-2015.) $)
    pconcn $p |- ( ( J e. PCon /\ A e. X /\ B e. X ) ->
      E. f e. ( II Cn J ) ( ( f ` 0 ) = A /\ ( f ` 1 ) = B ) ) $=
      ( vx vy cpcon wcel cc0 cv cfv wceq c1 wa wrex wral eqeq2 rexbidv cii ctop
      ccn co ispcon simprbi anbi1d anbi2d rcla42v syl5com 3impib ) DIJZAEJZBEJZ
      KCLZMZANZOUOMZBNZPZCUADUCUDZQZULUPGLZNZURHLZNZPZCVAQZHERGERZUMUNPVBULDUBJ
      VIGHCDEFUEUFVHVBUQVFPZCVAQGHABEEVCANZVGVJCVAVKVDUQVFVCAUPSUGTVEBNZVJUTCVA
      VLVFUSUQVEBURSUHTUIUJUK $.
      $( [11-Feb-2015] $)
  $}

  ${
    $d f F $.  $d f j x y J $.
    $( A simply connected space is a topology.  (Contributed by Mario Carneiro,
       11-Feb-2015.) $)
    pcontop $p |- ( J e. PCon -> J e. Top ) $=
      ( vf vx vy cpcon wcel ctop cc0 cv cfv wceq c1 cii ccn wrex cuni wral eqid
      wa co ispcon simplbi ) AEFAGFHBIZJCIKLUCJDIKSBMANTODAPZQCUDQCDBAUDUDRUAUB
      $.
      $( [11-Feb-2015] $)

    $( The property of being a simply connected topological space.
       (Contributed by Mario Carneiro, 11-Feb-2015.) $)
    isscon $p |- ( J e. SCon <-> ( J e. PCon /\ A. f e. ( II Cn J )
     ( ( f ` 0 ) = ( f ` 1 ) ->
       f ( ~=ph ` J ) ( ( 0 [,] 1 ) X. { ( f ` 0 ) } ) ) ) ) $=
      ( vj cc0 cv cfv c1 wceq cicc co csn cxp cphtpc wbr wi cii ccn cpcon cscon
      wral oveq2 fveq2 breqd imbi2d raleqbidv df-scon elrab2 ) DAEZFZGUHFHZUHDG
      IJUIKLZCEZMFZNZOZAPULQJZTUJUHUKBMFZNZOZAPBQJZTCBRSULBHZUOUSAUPUTULBPQUAVA
      UNURUJVAUMUQUHUKULBMUBUCUDUEACUFUG $.
      $( [11-Feb-2015] $)

    $( A simply connected space is path-connected.  (Contributed by Mario
       Carneiro, 11-Feb-2015.) $)
    sconpcon $p |- ( J e. SCon -> J e. PCon ) $=
      ( vf cscon wcel cpcon cc0 cv cfv c1 wceq co csn cxp cphtpc wbr wi cii ccn
      cicc wral isscon simplbi ) ACDAEDFBGZHZIUCHJUCFISKUDLMANHOPBQARKTBAUAUB
      $.
      $( [11-Feb-2015] $)

    $( A simply connected space is a topology.  (Contributed by Mario Carneiro,
       11-Feb-2015.) $)
    scontop $p |- ( J e. SCon -> J e. Top ) $=
      ( cscon wcel cpcon ctop sconpcon pcontop syl ) ABCADCAECAFAGH $.
      $( [11-Feb-2015] $)

    $( A closed path in a simply connected space is contractible to a point.
       (Contributed by Mario Carneiro, 11-Feb-2015.) $)
    sconpht $p |- ( ( J e. SCon /\ F e. ( II Cn J ) /\ ( F ` 0 ) = ( F ` 1 ) )
      -> F ( ~=ph ` J ) ( ( 0 [,] 1 ) X. { ( F ` 0 ) } ) ) $=
      ( vf cscon wcel cii ccn co cc0 cfv c1 wceq csn cxp cphtpc wbr cv wi fveq1
      cicc cpcon isscon simprbi eqeq12d id sneqd xpeq2d breq12d imbi12d rcla4cv
      wral syl 3imp ) BDEZAFBGHZEZIAJZKAJZLZAIKTHZUQMZNZBOJZPZUNICQZJZKVEJZLZVE
      UTVFMZNZVCPZRZCUOUKZUPUSVDRZRUNBUAEVMCBUBUCVLVNCAUOVEALZVHUSVKVDVOVFUQVGU
      RIVEASZKVEASUDVOVEAVJVBVCVOUEVOVIVAUTVOVFUQVPUFUGUHUIUJULUM $.
      $( [11-Feb-2015] $)
  $}

  ${
    $d x F $.  $d x ph $.  $d x U $.  $d x X $.
    concn.x $e |- X = U. J $.
    concn.j $e |- ( ph -> J e. Con ) $.
    concn.k $e |- ( ph -> K e. Top ) $.
    concn.f $e |- ( ph -> F e. ( J Cn K ) ) $.
    concn.u $e |- ( ph -> U e. K ) $.
    concn.c $e |- ( ph -> U e. ( Clsd ` K ) ) $.
    concn.a $e |- ( ph -> A e. X ) $.
    concn.1 $e |- ( ph -> ( F ` A ) e. U ) $.
    $( A continuous function from a connected topology with one point in a
       clopen set must lie entirely within the set. $)
    concn $p |- ( ph -> F : X --> U ) $=
      ( vx wcel syl syl2anc adantr wfn cv cfv wral wf cuni ctop ccn ccon contop
      co eqid cnf syl3anc ffn wa crn cop csubsp wfo cvv rnexg stoig3 dffn4 wceq
      sylib wb wss frn stoig2 foeq3 mpbid ssid cnsubsp2 syl22anc cnconn wn cdif
      a1i cin wne w3a cun ccld cldopn jca fnfvelrn inelcm simprl ffvelrn simprr
      c0 eldif sylanbrc disjdif undif2 elssuni ssequn1 syl5eq sseqtr4d nconsuba
      3jca syl23anc expr mt4d ralrimiva ffnfv ) ADGUAZPUBZDUCZCQZPGUDGCDUEAGFUF
      ZDUEZXHAEUGQZFUGQZDEFUHUKZQZXMAEUIQZXNIEUJRZJKDEFGXLHXLULZUMUNZGXLDUOZRZA
      XKPGAXIGQZUPDUQZFURUSUCZUIQZXKAYGYDAXRYFUGQZGYFUFZDUTZDEYFUHUKQZYGIAXOYEV
      AQZYHJAXQYLKDXPVBRYEFVAVCSAGYEDUTZYJAXHYMYCGDVDVFAYEYIVEZYMYJVGAXOYEXLVHZ
      YNJAXMYOYAGXLDVIRZYEFVJSYEYIGDVKRVLAXQYKKAXNXOYEYEVHZYOXQYKVGXSJYQAYEVMVS
      YPYEDEFXLXTVNVOVLDEYFGYIHYIULVPVOTAYDXKVQZYGVQZAYDYRUPZUPZXOYOCFQZXLCVRZF
      QZUPZCYEVTWLWAZUUCYEVTWLWAZCUUCVTWLVEZWBYECUUCWCZVHZYSAXOYTJTAYOYTYPTAUUE
      YTAUUBUUDLAXOCFWDUCQUUDJMCFXLXTWESWFTUUAUUFUUGUUHAUUFYTABDUCZCQUUKYEQZUUF
      OAXHBGQUULYCNGBDWGSUUKCYEWHSTUUAXJUUCQZXJYEQZUUGUUAXJXLQZYRUUMUUAXMYDUUOA
      XMYTYATZAYDYRWIZGXLXIDWJSAYDYRWKXJXLCWMWNUUAXHYDUUNUUAXMXHUUPYBRUUQGXIDWG
      SXJUUCYEWHSUUHUUACXLWOVSXBAUUJYTAYEXLUUIYPAUUICXLWCZXLCXLWPACXLVHZUURXLVE
      AUUBUUSLCFWQRCXLWRVFWSWTTYECFUUCXLXTXAXCXDXEXFPGCDXGWN $.
      $( [16-Feb-2015] $)
  $}

  ${
    $d a b f x y J $.  $d f g h t u v w x y z R $.  $d f g h t u v w x y z S $.
    $d f g i t x y z A $.  $d f g i t x y z F $.  $d g i t x y z V $.
    $( A path-connected space is connected.  (Contributed by Mario Carneiro,
       11-Feb-2015.) $)
    pconcon $p |- ( J e. PCon -> J e. Con ) $=
      ( vx vy va vb vf wcel cv c0 wne wceq wi wa wex cc0 cfv syl2anc idi adantr
      c1 cpcon ccon cin w3a cuni cun wral df-3an n0 anbi12i eeanv bitr4i cii co
      wrex simpll simprll simplrl elunii simprlr simplrr eqid pconcn syl3anc wn
      ccn adantl cicc iiuni iicon a1i ctop simplll pcontop syl cdif ccld simprr
      wf uncom syl6reqr wb elssuni incom syl5eq uneqdifeq mpbid opncld eqeltrrd
      0elunit eqeltrd concn 1elunit ffvelrn sylancl df-ne sylib pm2.65i pm2.21i
      wss inelcm expr pm2.01d sylibr rexlimdva exp32 exlimdvv syl5bi ralrimivva
      mpd imp3a dfcon2 mpbird ) AUAGZAUBGZBHZIJZCHZIJZXPXRUCZIKZUDZAUEZXPXRUFZJ
      ZLZCAUGBAUGZXNYFBCAAYBXQXSMZYAMXNXPAGZXRAGZMZMZYEXQXSYAUHYLYHYAYEYHDHZXPG
      ZEHZXRGZMZENDNZYLYAYELZYHYNDNZYPENZMYRXQYTXSUUADXPUIEXRUIUJYNYPDEUKULYLYQ
      YSDEYLYQYAYEYLYQYAMZMZOFHZPZYMKZTUUDPZYOKZMZFUMAVFUNZUOZYEUUCXNYMYCGZYOYC
      GZUUKXNYKUUBUPUUCYNYIUULYLYNYPYAUQZXNYIYJUUBURZYMXPAUSQUUCYPYJUUMYLYNYPYA
      UTZXNYIYJUUBVAZYOXRAUSQYMYOFAYCYCVBZVCVDUUCUUIYEFUUJUUCUUDUUJGZUUIYEUUCUU
      SUUIMZMZYCYDKZVEZYEUVAUVBUUCUUTUVBUVCUUCUUTUVBMZMZUVCUVEYAYLYQYAUVDVAZUVE
      XTIJZYAVEUVEYOXPGYPUVGUVEUUGYOXPUVDUUHUUCUUSUUFUUHUVBVAVGUVEOTVHUNZXPUUDV
      STUVHGUUGXPGUVEOXPUUDUMAUVHVIUMUBGUVEVJVKUVEXNAVLGZUVEXNLXNYKUUBUVDVMRAVN
      ZVOZUVEUUSLUUCUUSUUIUVBUQRUVEYILUUCYIUVDUUOSRUVEYCXRVPZXPAVQPZUVEXRXPUFZY
      CKZUVLXPKZUVEYCYDUVNUUCUUTUVBVRXRXPVTWAUVEXRYCWTZXRXPUCZIKUVOUVPWBUVEYJUV
      QUVEYJLUUCYJUVDUUQSRZXRAWCVOUVEUVRXTIXRXPWDUVEYALUVFRWEXRXPYCWFQWGUVEUVIY
      JUVLUVMGUVKUVSXRAYCUURWHQWIOUVHGUVEWJVKUVEUUEXPGLUVEUUEYMXPUVDUUFUUCUUSUU
      FUUHUVBURVGUUCYNUVDUUNSWKRWLWMUVHXPTUUDWNWOWIUUCYPUVDUUPSYOXPXRXAQXTIWPWQ
      WRWSXBXCYCYDWPXDXBXEXJXFXGXHXKXHXIXNUVIXOYGWBUVJBCAYCUURXLVOXM $.
      $( [11-Feb-2015] $)

    $( The topological product of two path-connected spaces is path-connected.
       (Contributed by Mario Carneiro, 12-Feb-2015.) $)
    txpcon $p |- ( ( R e. PCon /\ S e. PCon ) -> ( R tX S ) e. PCon ) $=
      ( vf vu vv vz vw vg vh vt wcel wa co cc0 cv cfv wceq c1 wrex wral vx ctop
      vy cpcon ctx cii ccn cuni pcontop txtop syl2an cxp cop w3a pconcn anim12i
      an6 eqid sylbir reeanv sylibr cicc cmpt simpl1l syl simpl1r iitop simprll
      a1i simprlr iiuni txcnmpt syl32anc 0elunit fveq2 opeq12d opex fvmpt ax-mp
      simprrl simpld simprrr syl5eq 1elunit simprd fveq1 eqeq1d anbi12d rcla4ev
      syl12anc expr rexlimdvva mpd 3expa ralrimivva eqeq2 anbi2d rexbidv anbi1d
      ralxp 2ralbidv syl5bb txuni raleqdv raleqbidv mpbid ispcon sylanbrc ) AUD
      KZBUDKZLZABUEMZUBKZNCOZPZDOZQZRXNPZEOZQZLZCUFXLUGMZSZEXLUHZTZDYDTZXLUDKXI
      AUBKZBUBKZXMXJAUIZBUIZABUJUKXKYCEAUHZBUHZULZTZDYMTZYFXKXOUAOZUCOZUMZQZXRF
      OZGOZUMZQZLZCYBSZGYLTFYKTZUCYLTUAYKTYOXKUUFUAUCYKYLXKYPYKKZYQYLKZLZLUUEFG
      YKYLXKUUIYTYKKZUUAYLKZLZUUEXKUUIUULUNZNHOZPZYPQZRUUNPZYTQZLZNIOZPZYQQZRUU
      TPZUUAQZLZLZIUFBUGMZSHUFAUGMZSZUUEUUMUUSHUVHSZUVEIUVGSZLZUVIUUMXIUUGUUJUN
      ZXJUUHUUKUNZLUVLXIUUGUUJXJUUHUUKUQUVMUVJUVNUVKYPYTHAYKYKURZUOYQUUAIBYLYLU
      RZUOUPUSUUSUVEHIUVHUVGUTVAUUMUVFUUEHIUVHUVGUUMUUNUVHKZUUTUVGKZLZUVFUUEUUM
      UVSUVFLZLZJNRVBMZJOZUUNPZUWCUUTPZUMZVCZYBKZNUWGPZYRQZRUWGPZUUBQZUUEUWAYGY
      HUFUBKZUVQUVRUWHUWAXIYGXIXJUUIUULUVTVDYIVEUWAXJYHXIXJUUIUULUVTVFYJVEUWMUW
      AVGVIUUMUVQUVRUVFVHUUMUVQUVRUVFVJJABUFUUNUUTUWGUWBVKUWGURZVLVMUWAUWIUUOUV
      AUMZYRNUWBKUWIUWOQVNJNUWFUWOUWBUWGUWCNQUWDUUOUWEUVAUWCNUUNVOUWCNUUTVOVPUW
      NUUOUVAVQVRVSUWAUUOYPUVAYQUWAUUPUURUUMUVSUUSUVEVTZWAUWAUVBUVDUUMUVSUUSUVE
      WBZWAVPWCUWAUWKUUQUVCUMZUUBRUWBKUWKUWRQWDJRUWFUWRUWBUWGUWCRQUWDUUQUWEUVCU
      WCRUUNVOUWCRUUTVOVPUWNUUQUVCVQVRVSUWAUUQYTUVCUUAUWAUUPUURUWPWEUWAUVBUVDUW
      QWEVPWCUUDUWJUWLLCUWGYBXNUWGQZYSUWJUUCUWLUWSXOUWIYRNXNUWGWFWGUWSXRUWKUUBR
      XNUWGWFWGWHWIWJWKWLWMWNWOWOYNUUFDUAUCYKYLYNXQUUCLZCYBSZGYLTFYKTXPYRQZUUFY
      CUXAEFGYKYLXSUUBQZYAUWTCYBUXCXTUUCXQXSUUBXRWPWQWRWTUXBUXAUUEFGYKYLUXBUWTU
      UDCYBUXBXQYSUUCXPYRXOWPWSWRXAXBWTVAXKYNYEDYMYDXIYGYHYMYDQXJYIYJABYKYLUVOU
      VPXCUKZXKYCEYMYDUXDXDXEXFDECXLYDYDURXGXH $.
      $( [12-Feb-2015] $)

    $( The topological product of a collection of path-connected spaces is
       path-connected.  (Contributed by Mario Carneiro, 12-Feb-2015.) $)
    ptpcon $p |- ( ( A e. V /\ F : A --> PCon ) -> ( Xt_ ` F ) e. PCon ) $=
      ( vf vx vt vz vi wcel cpcon wa cfv ctop cc0 cv wceq c1 cii wral cmpt wrex
      vy vg wf cpt ccn co cuni wss pcontop ssriv fss mpan2 pttop sylan2 cid wfn
      wex fvi ad2antrr eleq2d biimpa simplr sylan cixp simprl eqid ptuni adantr
      ffvelrn eleqtrrd elixp sylib simprd r19.21bi simprr pconcn syl3anc df-rex
      vex syldan ralrimiva fvex eleq1 fveq1 eqeq1d anbi12d ac6s2 syl cicc iiuni
      iitop simplll biimpar fveq2 oveq2d eleq12d fveq1d eqeq12d rcla4cva simpld
      a1i cnf feqmptd eqeltrrd ptcn mpteq2dva cvv 0elunit mptexg fvmptg sylancr
      mpteq2dv dffn5v 3eqtr4d 1elunit rcla4ev syl12anc ex mpd ralrimivva ispcon
      exlimdv sylanbrc ) ACIZAJBUDZKZBUELZMIZNDOZLZEOZPZQYJLZUBOZPZKZDRYHUFUGZU
      AZUBYHUHZSEYTSYHJIYFYEAMBUDZYIYFJMUIUUAEJMYLUJUKAJMBULUMZABCUNUOYGYSEUBYT
      YTYGYLYTIZYOYTIZKZKZUCOZAUPLZUQZFOZUUGLZRUUJBLZUFUGZIZNUUKLZUUJYLLZPZQUUK
      LZUUJYOLZPZKZKZFUUHSZKZUCURZYSUUFYJUUMIZYKUUPPZYNUUSPZKZKZDURZFUUHSUVEUUF
      UVKFUUHUUFUUJUUHIZUUJAIZUVKUUFUVLUVMUUFUUHAUUJYEUUHAPZYFUUEACUSUTZVAVBUUF
      UVMKZUVIDUUMUAZUVKUVPUULJIZUUPUULUHZIZUUSUVSIZUVQUUFYFUVMUVRYEYFUUEVCZAJU
      UJBVJVDUUFUVTFAUUFYLAUQZUVTFASZUUFYLFAUVSVEZIUWCUWDKUUFYLYTUWEYGUUCUUDVFY
      GUWEYTPZUUEYFYEUUAUWFUUBFABYHCYHVGZVHUOVIZVKFAUVSYLEVTVLVMZVNVOUUFUWAFAUU
      FYOAUQZUWAFASZUUFYOUWEIUWJUWKKUUFYOYTUWEYGUUCUUDVPUWHVKFAUVSYOUBVTVLVMZVN
      VOUUPUUSDUULUVSUVSVGVQVRUVIDUUMVSVMWAWBUVJUVBFDUUHUCAUPWCYJUUKPZUVFUUNUVI
      UVAYJUUKUUMWDUWMUVGUUQUVHUUTUWMYKUUOUUPNYJUUKWEWFUWMYNUURUUSQYJUUKWEWFWGW
      GWHWIUUFUVDYSUCUUFUVDYSUUFUVDKZGNQWJUGZHAGOZHOZUUGLZLZTZTZYRINUXALZYLPZQU
      XALZYOPZYSUWNGUWSHBARYHCUWOWKUWGRMIZUWNWLXBYEYFUUEUVDWMZUWNYFUUAUUFYFUVDU
      WBVIUUBWIZUWNUWQAIZKZUWRGUWOUWSTRUWQBLZUFUGZUXJGUWOUXKUHZUWRUXJUXFUXKMIZU
      WRUXLIZUWOUXMUWRUDUXFUXJWLXBUWNUUAUXIUXNUXHAMUWQBVJVDUXJUXONUWRLZUWQYLLZP
      ZQUWRLZUWQYOLZPZKZUWNUXIUWQUUHIZUXOUYBKZUWNUYCUXIUWNUUHAUWQUUFUVNUVDUVOVI
      VAWNUWNUVCUYCUYDUUFUUIUVCVPUVBUYDFUWQUUHUUJUWQPZUUNUXOUVAUYBUYEUUKUWRUUMU
      XLUUJUWQUUGWOZUYEUULUXKRUFUUJUWQBWOWPWQUYEUUQUXRUUTUYAUYEUUOUXPUUPUXQUYEN
      UUKUWRUYFWRUUJUWQYLWOWSUYEUURUXSUUSUXTUYEQUUKUWRUYFWRUUJUWQYOWOWSWGWGWTVD
      WAZXAZUWRRUXKUWOUXMWKUXMVGXCVRXDUYHXEXFUWNHAUXPTZHAUXQTZUXBYLUWNHAUXPUXQU
      XJUXRUYAUXJUXOUYBUYGVNZXAXGUWNNUWOIUYIXHIZUXBUYIPXIUWNYEUYLUXGHAUXPCXJWIG
      NUWTUYIUWOXHUXAUWPNPHAUWSUXPUWPNUWRWOXMUXAVGZXKXLUWNUWCYLUYJPUUFUWCUVDUUF
      UWCUWDUWIXAVIHAYLXNVMXOUWNHAUXSTZHAUXTTZUXDYOUWNHAUXSUXTUXJUXRUYAUYKVNXGU
      WNQUWOIUYNXHIZUXDUYNPXPUWNYEUYPUXGHAUXSCXJWIGQUWTUYNUWOXHUXAUWPQPHAUWSUXS
      UWPQUWRWOXMUYMXKXLUWNUWJYOUYOPUUFUWJUVDUUFUWJUWKUWLXAVIHAYOXNVMXOYQUXCUXE
      KDUXAYRYJUXAPZYMUXCYPUXEUYQYKUXBYLNYJUXAWEWFUYQYNUXDYOQYJUXAWEWFWGXQXRXSY
      CXTYAEUBDYHYTYTVGYBYD $.
      $( [17-Feb-2015] $)
  $}

  ${
    $d f g h s t x y $.  $d f h s y A $.  $d f g P $.  $d f g Q $.  $d f g S $.
    $d f h s y B $.  $d f g h s t y J $.  $d f g T $.  $d f h s t y X $.
    pconpi1.x $e |- X = U. J $.
    pconpi1.p $e |- P = ( J pi1 A ) $.
    pconpi1.q $e |- Q = ( J pi1 B ) $.
    pconpi1.s $e |- S = ( Base ` P ) $.
    pconpi1.t $e |- T = ( Base ` Q ) $.
    $( All fundamental groups in a path-connected space are isomorphic.
       (Contributed by Mario Carneiro, 12-Feb-2015.) $)
    pconpi1 $p |- ( ( J e. PCon /\ A e. X /\ B e. X ) ->
      E. g e. ( P GrpHom Q ) g : S -1-1-onto-> T ) $=
      ( wcel cv cfv wceq c1 co vf vs vy vx vh vt cpcon w3a cc0 wa cii wrex wf1o
      ccn cghm pconcn cpi1 cbs cicc cmin cmpt cpco cphtpc crab eqid ctop simpl1
      cec cio pcontop simprl oveq2 fveq2d cbvmptv pi1xfr simprrl oveq2d syl6eqr
      syl simprrr oveq12d eleqtrd pi1xfrf1o wb f1oeq2 f1oeq3 bitrd mpbid f1oeq1
      rcla4ev syl2anc expr rexlimdva mpd ) HUGOZAIOZBIOZUHZUIUAPZQZARZSWSQZBRZU
      JZUAUKHUNTZULEFGPZUMZGCDUOTZULZABUAHIJUPWRXDXIUAXEWRWSXEOZXDXIWRXJXDUJZUJ
      ZUBHWTUQTZURQZUCPZUDUISUSTZSUDPZUTTZWSQZVAZUEPWSHVBQZTYATHVCQVHRUEUBPULUC
      VIVAZXHOEFYBUMZXIXLYBXMHXBUQTZUOTXHXLUCUFXMYDUIUFPZQWTRSYEQWTRUJUFXEVDZUE
      WSYBXTHIUBJXMVEZYDVEZYBVEZYFVEZXLWOHVFOWOWPWQXKVGHVJVSZWRXJXDVKZUDUCXPXSS
      XOUTTZWSQXQXORXRYMWSXQXOSUTVLVMVNZVOXLXMCYDDUOXLXMHAUQTCXLWTAHUQWRXJXAXCV
      PVQKVRZXLYDHBUQTDXLXBBHUQWRXJXAXCVTVQLVRZWAWBXLXNYDURQZYBUMZYCXLUCUFXMYDY
      FUEWSYBXTHIUBJYGYHYIYJYKYLYNWCXLYREYQYBUMZYCXLXNERYRYSWDXLXNCURQEXLXMCURY
      OVMMVRXNEYQYBWEVSXLYQFRYSYCWDXLYQDURQFXLYDDURYPVMNVRYQFEYBWFVSWGWHXGYCGYB
      XHEFXFYBWIWJWKWLWMWN $.
      $( [12-Feb-2015] $)
  $}

  ${
    $d x G $.  $d x J $.
    sconpht2.1 $e |- ( ph -> J e. SCon ) $.
    sconpht2.2 $e |- ( ph -> F e. ( II Cn J ) ) $.
    sconpht2.3 $e |- ( ph -> G e. ( II Cn J ) ) $.
    sconpht2.4 $e |- ( ph -> ( F ` 0 ) = ( G ` 0 ) ) $.
    sconpht2.5 $e |- ( ph -> ( F ` 1 ) = ( G ` 1 ) ) $.
    $( Any two paths in a simply connected space with the same start and end
       point are path-homotopic.  (Contributed by Mario Carneiro,
       12-Feb-2015.) $)
    sconpht2 $p |- ( ph -> F ( ~=ph ` J ) G ) $=
      ( vx cc0 c1 co cfv csn cxp wbr wcel wceq wi cicc cv cmin cmpt cpco cphtpc
      cscon cii ccn ctop scontop syl w3a idi eqid pcorevcl simp1d simp2d eqtr4d
      syl2anc pcocn simp3d 3eqtr4d sconpht syl3anc sneqd xpeq2d breqtrd pcophtb
      pco0 pco1 mpbid ) ABJKLUAMZLJUBUCMCNUDZDUENMZVMKBNZOZPZDUFNZQBCVSQAVOVMKV
      ONZOZPZVRVSADUGRZVOUHDUIMZRVTLVONZSVOWBVSQEABVNDAWCDUJRZEDUKULZFAVNWDRZKV
      NNZLCNZSZLVNNZKCNZSZAWFCWDRZWHWKWNUMAWFTWGUNAWOTGUNJCVNDVNUOZUPUTZUQZALBN
      WJWIIAWHWKWNWQURUSVAAVPWLVTWEAVPWMWLHAWHWKWNWQVBUSABVNDWGFWRVJZABVNDWGFWR
      VKVCVODVDVEAWAVQVMAVTVPWSVFVGVHAJVRBCVNDWPVRUOWGFGHIVIVL $.
      $( [12-Feb-2015] $)
  $}

  ${
    $d f g x J $.  $d f x X $.  $d f g x Y $.
    sconpi1.1 $e |- X = U. J $.
    $( A path-connected topological space is simply connected iff its
       fundamental group is trivial.  (Contributed by Mario Carneiro,
       12-Feb-2015.) $)
    sconpi1 $p |- ( ( J e. PCon /\ Y e. X ) ->
      ( J e. SCon <-> ( Base ` ( J pi1 Y ) ) ~~ 1o ) ) $=
      ( vf vg wcel wa co cfv c1o cen wbr csn cc0 wceq c1 eqid syl2anc syl cpcon
      vx cscon cpi1 cbs c0g cv cphtpc cec cii wrex ctop wb scontop adantl simpl
      ccn elpi1 cicc cxp cvv wer ad2antrr phtpcer simplr ovex snex xpex simpllr
      a1i simprl simprr eqtr4d sconpht syl3anc sneqd xpeq2d breqtrd erthi pi1id
      elsn eqeq1 syl5bb syl5ibrcom expimpd rexlimdva sylbid ssrdv pi1gp grpidcl
      cgrp snssd eqssd fvex ensn1 syl6eqbr adantll wi simpll simplll pcontop wf
      wral iitop iiuni cnf 0elunit ffvelrn sylancl eqidd eqcomd elpi1i syl23anc
      w3a cuni syl6eleq pcoptcl syl21anc wf1o cghm pconpi1 f1oen2g mpan en1eqsn
      vex rexlimivw entr eleqtrd elsni cdm phtpcdm simp1d erth mpbird ralrimiva
      expr isscon sylanbrc impbida ) AUAGZCBGZHZAUCGZACUDIZUEJZKLMZUUAUUCUUFYTU
      UAUUCHZUUEUUDUFJZNZKLUUGUUEUUIUUGUBUUEUUIUUGUBUGZUUEGZOEUGZJZCPZQUULJZCPZ
      HZUUJUULAUHJZUIZPZHZEUJAUQIZUKZUUJUUIGZUUGAULGZUUAUUKUVCUMUUCUVEUUAAUNUOZ
      UUAUUCUPZUUEEUUJUUDABCDUUDRZUUERZURSUUGUVAUVDEUVBUUGUULUVBGZHZUUQUUTUVDUV
      KUUQHZUVDUUTUUSUUHPZUVLUUSOQUSIZCNZUTZUURUIZUUHUVLUULUVPUURUVBVAUVLUVEUUR
      VBZUUGUVEUVJUUQUVFVCAVDZTUUGUVJUUQVEZUVPVAGUVLUVNUVOOQUSVFCVGVHVJUVLUULUV
      NUUMNZUTZUVPUURUVLUUCUVJUUMUUOPZUULUWBUURMZUUAUUCUVJUUQVIUVTUVLUUMCUUOUVK
      UUNUUPVKZUVKUUNUUPVLVMUULAVNVOUVLUWAUVOUVNUVLUUMCUWEVPVQVRVSUUGUUHUVQPZUV
      JUUQUUGUVEUUAUWFUVFUVGUUDABCUUHDUVHUUHRZVTSVCVMUVDUUJUUHPUUTUVMUBUUHWAUUJ
      UUSUUHWBWCWDWEWFWGWHUUGUUHUUEUUGUUDWKGZUUHUUEGUUGUVEUUAUWHUVFUVGUUDABCDUV
      HWISUUEUUDUUHUVIUWGWJTWLWMUUHUUDUFWNWOWPWQUUBUUFHZYTUWCUWDWRZEUVBXCUUCYTU
      UAUUFWSUWIUWJEUVBUWIUVJUWCUWDUWIUVJUWCHZHZUWDUUSUWBUURUIZPZUWLUUSUWMNZGUW
      NUWLUUSAUUMUDIZUEJZUWOUWLUVEUUMBGZUVJUUMUUMPUUOUUMPUUSUWQGUWLYTUVEYTUUAUU
      FUWKWTZAXATZUWLUVNBUULXBZOUVNGUWRUWLUJULGZUVEUVJUXAUXBUWLXDVJUWTUWIUVJUWC
      VKZUULUJAUVNBXEDXFVOXGUVNBOUULXHXIZUXCUWLUUMXJUWLUUMUUOUWIUVJUWCVLXKUWQUU
      LUWPABUUMDUWPRZUWQRZXLXMUWLUWMUWQGZUWQKLMZUWQUWOPUWLUVEUWRUWBUVBGZOUWBJUU
      MPZQUWBJUUMPZXNZUXGUWTUXDUWLUVEUUMAXOZGUXLUWTUWLUUMBUXMUXDDXPUWBAUUMUWBRX
      QSZUWQUWBUWPABUUMDUXEUXFXLXRUWLUWQUUELMZUUFUXHUWLUWQUUEFUGZXSZFUWPUUDXTIZ
      UKZUXOUWLYTUWRUUAUXSUWSUXDYTUUAUUFUWKVIUUMCUWPUUDUWQUUEFABDUXEUVHUXFUVIYA
      VOUXQUXOFUXRUXPVAGUXQUXOFYEUWQUUEVAUXPYBYCYFTUUBUUFUWKVEUWQUUEKYGSUWMUWQY
      DSYHUUSUWMYITUWLUULUWBUURUVBUVBUWLUVEUVRUWTUVSTUWLUVEUURYJUVBPUWTAYKTUXCU
      WLUXIUXJUXKUXNYLYMYNYPYOEAYQYRYS $.
      $( [12-Feb-2015] $)
  $}

  ${
    $d t z J $.  $d f s t x y z K $.  $d f s t x y z ph $.  $d t x y z S $.
    cvxpcon.1 $e |- ( ph -> S C_ CC ) $.
    cvxpcon.2 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ t e. ( 0 [,] 1 ) ) ) ->
      ( ( t x. x ) + ( ( 1 - t ) x. y ) ) e. S ) $.
    cvxpcon.3 $e |- J = ( MetOpen ` ( abs o. - ) ) $.
    cvxpcon.4 $e |- K = ( subSp ` <. S , J >. ) $.
    $( A convex subset of the complex numbers is path-connected.  (Contributed
       by Mario Carneiro, 12-Feb-2015.) $)
    cvxpcon $p |- ( ph -> K e. PCon ) $=
      ( wcel cc0 cfv wceq c1 co cc cmul caddc vf ctop cv cii ccn wrex cuni wral
      cpcon cop csubsp cvv cabs cmin ccom cme eqid cnmet opntop ax-mp wss ssexg
      cnex sylancl stoig3 sylancr syl5eqel cicc cmpt cnmetba uniopn2 copn dfii3
      wa opeq2i fveq2i eqtr4i a1i 0re 1re iccssre mp2an ax-resscn sstri cnmptid
      cr adantr simprr sseldd cnmptc ctx mulcn cnmpt12f sselii simprl cnmpt1res
      subcn addcn crn wb iitop wf 3exp2 com23 imp42 fmptd frn cnsubsp2 syl22anc
      wi syl mpbid oveq2i syl6eleqr 0elunit oveq1 subid1i syl6eq oveq1d oveq12d
      oveq2 fvmpt mul02 mulid2 addid2 eqtrd syl5eq 1elunit subidi addid1 eqeq1d
      ovex fveq1 anbi12d rcla4ev syl12anc ralrimivva syl6sseq stoig2 unieqi
      syl6eqr raleqdv raleqbidv ispcon sylanbrc ) AGUBLMUAUCZNZCUCZOZPUUFNZBUCZ
      OZVNZUAUDGUEQZUFZBGUGZUHZCUUPUHZGUILAGEFUJUKNZUBKAFUBLZEULLZUUSUBLUMUNUOZ
      UPLZUUTUVBUVBUQZURZUVBFJUSUTZAERVAZRULLUVAHVCERULVBVDEFULVEVFVGAUUOBEUHZC
      EUHUURAUUOCBEEAUUHELZUUKELZVNZVNZDMPVHQZDUCZUUKSQZPUVNUNQZUUHSQZTQZVIZUUN
      LMUVSNZUUHOZPUVSNZUUKOZUUOUVLUVSUDUUSUEQZUUNUVLUVSUDFUEQLZUVSUWDLZUVLDUVR
      FUDFRUVMUVCRFUGZOUVEUVBFRUVBUVDVJJVKUTZUDUVMUVBVLNZUJZUKNUVMFUJZUKNVMUWKU
      WJUKFUWIUVMJVOVPVQUUTUVLUVFVRZUWLUVMRVAUVLUVMWFRMWFLPWFLUVMWFVAVSVTMPWAWB
      WCWDVRUVLDUVOUVQTFFFFRRUWHUWLUWHUWLUWLUVLDUVNUUKSFFFFRRUWHUWLUWHUWLUWLUVL
      DFRUWHUWLWEZUVLDUUKFFRRUWHUWLUWHUWLUVLERUUKAUVGUVKHWGZAUVIUVJWHWIZWJUWLSF
      FWKQFUEQZLUVLUVBFUVDJWLVRZWMUVLDUVPUUHSFFFFRRUWHUWLUWHUWLUWLUVLDPUVNUNFFF
      FRRUWHUWLUWHUWLUWLUVLDPFFRRUWHUWLUWHUWLPRLUVLWFRPWCVTWNZVRWJUWMUWLUNUWPLU
      VLUVBFUVDJWQVRWMUVLDUUHFFRRUWHUWLUWHUWLUVLERUUHUWNAUVIUVJWOWIZWJUWLUWQWMU
      WLTUWPLUVLUVBFUVDJWRVRWMWPUVLUDUBLZUUTUVSWSEVAZUVGUWEUWFWTUWTUVLXAVRUWLUV
      LUVMEUVSXBUXAUVLDUVMUVREUVSAUVIUVJUVNUVMLZUVRELZAUVJUVIUXBUXCXJAUVJUVIUXB
      UXCIXCXDXEUVSUQZXFUVMEUVSXGXKUWNEUVSUDFRUWHXHXIXLGUUSUDUEKXMXNUVLUVTMUUKS
      QZPUUHSQZTQZUUHMUVMLUVTUXGOXODMUVRUXGUVMUVSUVNMOZUVOUXEUVQUXFTUVNMUUKSXPU
      XHUVPPUUHSUXHUVPPMUNQPUVNMPUNYAPUWRXQXRXSXTUXDUXEUXFTYLYBUTUVLUXGMUUHTQZU
      UHUVLUXEMUXFUUHTUVLUUKRLZUXEMOUWOUUKYCXKUVLUUHRLZUXFUUHOUWSUUHYDXKXTUVLUX
      KUXIUUHOUWSUUHYEXKYFYGUVLUWBPUUKSQZMUUHSQZTQZUUKPUVMLUWBUXNOYHDPUVRUXNUVM
      UVSUVNPOZUVOUXLUVQUXMTUVNPUUKSXPUXOUVPMUUHSUXOUVPPPUNQMUVNPPUNYAPUWRYIXRX
      SXTUXDUXLUXMTYLYBUTUVLUXNUUKMTQZUUKUVLUXLUUKUXMMTUVLUXJUXLUUKOUWOUUKYDXKU
      VLUXKUXMMOUWSUUHYCXKXTUVLUXJUXPUUKOUWOUUKYJXKYFYGUUMUWAUWCVNUAUVSUUNUUFUV
      SOZUUIUWAUULUWCUXQUUGUVTUUHMUUFUVSYMYKUXQUUJUWBUUKPUUFUVSYMYKYNYOYPYQAUVH
      UUQCEUUPAEUUSUGZUUPAUUTEUWGVAEUXROUVFAERUWGHUWHYREFYSVFGUUSKYTUUAZAUUOBEU
      UPUXSUUBUUCXLCBUAGUUPUUPUQUUDUUE $.
      $( [12-Feb-2015] $)

    $( A convex subset of the complex numbers is simply connected.
       (Contributed by Mario Carneiro, 12-Feb-2015.) $)
    cvxscon $p |- ( ph -> K e. SCon ) $=
      ( wcel cc0 c1 wceq co cii cmul caddc cc vf vz vs cpcon cv cfv cicc cphtpc
      csn cxp wbr wi ccn wral cscon cvxpcon cphtpy wne simprl ctop cuni pcontop
      wa c0 w3a syl adantr iitop a1i iiuni eqid syl3anc 0elunit ffvelrn sylancl
      wf cnf pcoptcl syl2anc simp1d jca simp2d eqcomd simp3d simprr eqtr2d cmin
      jca32 cmpt2 ctx cop csubsp cabs ccom cme cnmet ax-mp cnmetba uniopn2 copn
      opntop dfii3 opeq2i fveq2i eqtr4i wss 0re iccssre mp2an cnmpt2res sylancr
      cr 1re eleqtrrd cnmpt2c cnmpt22f ax-1cn oveq2i syl22anc ralrimivva oveq1d
      oveq2 eleq1d oveq2d simpr syl6eq simpl fveq2d oveq12d ovex ovmpt2a mulid2
      wb mul02 3eqtrd 1elunit 3eqtr3d eqtrd ralrimiva mpbir2and ax-resscn sstri
      cnmpt2nd syl6sseq stoig2 unieqi syl6eqr mulcn cnmpt1st syl6eleq cnsubsp2r
      sseldd subcn cnmpt21f addcn 3exp2 imp42 an32s ad2ant2rl rcla42va syl21anc
      crn txtopi fmpt2 sylib frn cnsubsp2 mpbid syl6eleqr subid1i addid2 subidi
      sylan addid1 fvex fvconst2 adantl eqtr4d sseldi pncan3 subcl isphtpy ne0i
      adddir isphtpc expr isscon sylanbrc ) AGUDLZMUAUEZUFZNUWJUFZOZUWJMNUGPZUW
      KUIUJZGUHUFUKZULZUAQGUMPZUNGUOLABCDEFGHIJKUPZAUWQUAUWRAUWJUWRLZUWMUWPAUWT
      UWMVCZVCZUWPUWTUWOUWRLZVCZUWKMUWOUFZOZUWLNUWOUFZOZVCVCZUWJUWOGUQUFPZVDURZ
      UXBUXDUXFUXHUXBUWTUXCAUWTUWMUSZUXBUXCUXEUWKOZUXGUWKOZUXBGUTLZUWKGVAZLZUXC
      UXMUXNVEAUXOUXAAUWIUXOUWSGVBVFVGZUXBUWNUXPUWJVPZMUWNLZUXQUXBQUTLZUXOUWTUX
      SUYAUXBVHVIZUXRUXLUWJQGUWNUXPVJUXPVKVQVLZVMUWNUXPMUWJVNVOZUWOGUWKUWOVKVRV
      SZVTZWAUXBUXEUWKUXBUXCUXMUXNUYEWBWCZUXBUXGUWKUWLUXBUXCUXMUXNUYEWDAUWTUWMW
      EZWFZWHUXBUBDUWNUWNDUEZUWKRPZNUYJWGPZUBUEZUWJUFZRPZSPZWIZUXJLZUXKUXBUYRUY
      QQQWJPZGUMPZLUCUEZMUYQPZVUAUWJUFZOZVUANUYQPZVUAUWOUFZOZVCZMVUAUYQPZUWKOZN
      VUAUYQPZUWLOZVCVCZUCUWNUNUXBUYQUYSEFWKWLUFZUMPZUYTUXBUYQUYSFUMPLZUYQVUOLZ
      UXBUBDUYKUYOSQQFFFTUWNUWNTVJVJUYBUYBFUTLZUXBWMWGWNZWOLZVURVUSVUSVKZWPZVUS
      FJXAWQZVIZVVDUXBUBDUYJUWKRQQFFFTUWNUWNTVJVJUYBUYBVVDVVDUXBUBDUYJFQFFQUWNT
      UWNTVUTTFVAZOVVBVUSFTVUSVVAWRJWSWQZQUWNVUSWTUFZWKZWLUFUWNFWKZWLUFXBVVIVVH
      WLFVVGUWNJXCXDXEZVVDVVDUWNTXFUXBUWNXLTMXLLNXLLUWNXLXFXGXMMNXHXIUUAUUBZVIZ
      VVFVVJVVDVVLUXBUBDFFTTVVFVVFVVDVVDUUCZXJUXBUBDUWKQQFUWNUWNTVJVJUYBUYBVVDV
      VFUXBETUWKAETXFZUXAHVGZUXBUWKUXPEUYDAEUXPOZUXAAEVUNVAZUXPAVUREVVEXFEVVQOV
      VCAETVVEHVVFUUDEFUUEXKGVUNKUUFUUGVGZXNZUULZXOVVFVVFVVDRFFWJPFUMPZLUXBVUSF
      VVAJUUHVIZXPUXBUBDUYLUYNRQQFFFTUWNUWNTVJVJUYBUYBVVDVVDUXBUBDUYLFQFFQUWNTU
      WNTVVFVVJVVDVVDVVLVVFVVJVVDVVLUXBUBDNUYJWGFFFFFTTTTVVFVVFVVDVVDVVDVVDUXBU
      BDNFFFTTTVVFVVFVVDVVDVVDVVFNTLZUXBXQVIXOVVMVVFVVFVVDWGVWALUXBVUSFVVAJUUMV
      IXPXJUXBUBDUYMUWJQQQFUWNUWNVJVJUYBUYBUYBVVDUXBUBDQQUWNUWNVJVJUYBUYBUUIUXB
      UYAVURVVNUWJQVUNUMPZLUWJQFUMPLZUYBVVDVVOUXBUWJUWRVWDUXLGVUNQUMKXRUUJEUWJQ
      FTVVFUUKXSZUUNVVFVVFVVDVWBXPVVFVVFVVDSVWALUXBVUSFVVAJUUOVIXPUXBUYSUTLZVUR
      UYQUVBEXFZVVNVUPVUQYMVWGUXBQQVHVHUVCVIVVDUXBUWNUWNUJZEUYQVPZVWHUXBUYPELZD
      UWNUNUBUWNUNVWJUXBVWKUBDUWNUWNUXBUYMUWNLZUYJUWNLZVCZVCZUWKELZUYNELUYJBUEZ
      RPZUYLCUEZRPZSPZELZCEUNBEUNZVWKUXBVWPVWNVVSVGVWOUYNUXPEVWOUXSVWLUYNUXPLUX
      BUXSVWNUYCVGUXBVWLVWMUSUWNUXPUYMUWJVNVSUXBVVPVWNVVRVGXNAVWMVXCUXAVWLAVWMV
      CVXBBCEEAVWQELZVWSELZVCVWMVXBAVXDVXEVWMVXBAVXDVXEVWMVXBIUUPUUQUURXTUUSVXB
      VWKUYKVWTSPZELBCUWKUYNEEVWQUWKOZVXAVXFEVXGVWRUYKVWTSVWQUWKUYJRYBYAYCVWSUY
      NOZVXFUYPEVXHVWTUYOUYKSVWSUYNUYLRYBYDYCUUTUVAXTUBDUWNUWNUYPEUYQUYQVKZUVDU
      VEVWIEUYQUVFVFVVOEUYQUYSFTVVFUVGXSUVHGVUNUYSUMKXRUVIUXBVUMUCUWNUXBVUAUWNL
      ZVCZVUHVUJVULVXKVUDVUGVXKVUBMUWKRPZNVUCRPZSPZMVUCSPZVUCVXKVXJUXTVUBVXNOUX
      BVXJYEZVMUBDVUAMUWNUWNUYPVXNUYQUYMVUAOZUYJMOZVCZUYKVXLUYOVXMSVXSUYJMUWKRV
      XQVXRYEZYAVXSUYLNUYNVUCRVXSUYLNMWGPNVXSUYJMNWGVXTYDNXQUVJYFVXSUYMVUAUWJVX
      QVXRYGYHYIYIVXIVXLVXMSYJYKVOVXKVXLMVXMVUCSVXKUWKTLZVXLMOUXBVYAVXJVVTVGZUW
      KYNVFVXKVUCTLZVXMVUCOUXBUWNTUWJVPZVXJVYCUXBUYAVURVWEVYDUYBVVDVWFUWJQFUWNT
      VJVVFVQVLUWNTVUAUWJVNUVMZVUCYLVFYIVXKVYCVXOVUCOVYEVUCUVKVFYOVXKVUENUWKRPZ
      MVUCRPZSPZUWKMSPZVUFVXKVXJNUWNLZVUEVYHOVXPYPUBDVUANUWNUWNUYPVYHUYQVXQUYJN
      OZVCZUYKVYFUYOVYGSVYLUYJNUWKRVXQVYKYEZYAVYLUYLMUYNVUCRVYLUYLNNWGPMVYLUYJN
      NWGVYMYDNXQUVLYFVYLUYMVUAUWJVXQVYKYGYHYIYIVXIVYFVYGSYJYKVOVXKVYFUWKVYGMSV
      XKVYAVYFUWKOVYBUWKYLVFZVXKVYCVYGMOVYEVUCYNVFYIVXKVYIUWKVUFVXKVYAVYIUWKOVY
      BUWKUVNVFVXJVUFUWKOUXBUWNUWKVUAMUWJUVOUVPUVQUVRYOWAVXKVUIVUAUWKRPZNVUAWGP
      ZUWKRPZSPZUWKVXKUXTVXJVUIVYROVMVXPUBDMVUAUWNUWNUYPVYRUYQUYMMOZUYJVUAOZVCZ
      UYKVYOUYOVYQSWUAUYJVUAUWKRVYSVYTYEZYAWUAUYLVYPUYNUWKRWUAUYJVUANWGWUBYDWUA
      UYMMUWJVYSVYTYGYHYIYIVXIVYOVYQSYJYKXKVXKVUAVYPSPZUWKRPZVYFVYRUWKVXKWUCNUW
      KRVXKVUATLZVWCWUCNOVXKUWNTVUAVVKVXPUVSZXQVUANUVTVOYAVXKWUEVYPTLZVYAWUDVYR
      OWUFVXKVWCWUEWUGXQWUFNVUAUWAXKVYBVUAVYPUWKUWDVLVYNYQZYRVXKVUKVYOVYPUWLRPZ
      SPZUWLVXKVYJVXJVUKWUJOYPVXPUBDNVUAUWNUWNUYPWUJUYQUYMNOZVYTVCZUYKVYOUYOWUI
      SWULUYJVUAUWKRWUKVYTYEZYAWULUYLVYPUYNUWLRWULUYJVUANWGWUMYDWULUYMNUWJWUKVY
      TYGYHYIYIVXIVYOWUISYJYKXKVXKVYRUWKWUJUWLWUHVXKVYQWUIVYOSVXKUWKUWLVYPRUXBU
      WMVXJUYHVGZYDYDWUNYQYRWHYSUXBUWJUWOUYQGUCUXRUXLUYFUYGUYIUWBYTUXJUYQUWCVFU
      XBUXOUXCUWPUXIUXKVCYMUXRUYFUWJUWOGUWRUWEVSYTUWFYSUAGUWGUWH $.
      $( [12-Feb-2015] $)
  $}

  ${
    blcvx.d $e |- D = ( abs o. - ) $.
    blcvx.j $e |- J = ( MetOpen ` D ) $.
    blcvx.s $e |- S = ( P ( ball ` D ) R ) $.
    $( An open ball in the complex numbers is a convex set.  (Contributed by
       Mario Carneiro, 12-Feb-2015.) $)
    blcvx $p |- ( ( ( P e. CC /\ R e. RR+ ) /\
        ( A e. S /\ B e. S /\ T e. ( 0 [,] 1 ) ) ) ->
      ( ( T x. A ) + ( ( 1 - T ) x. B ) ) e. S ) $=
      ( cc wcel cc0 c1 co cmul clt syl2anc wceq crp cicc w3a cmin caddc cbl cfv
      wa wbr cr cle simpr3 0re elicc2i sylib simp1d recnd simpr1 syl6eleq cnmet
      1re cme a1i simpll simplr cnmetba elbl syl3anc mpbid simpld mulcl resubcl
      sylancr simpr2 addcl cabs cnmetdval subdi oveq12d addsub4 syl22anc ax-1cn
      wb pncan3 sylancl oveq1d adddir mulid2 3eqtr3d 3eqtr2d fveq2d subcl abscl
      syl eqtr4d readdcl rpre abstri oveq1 mul02 sylan9eqr syl6eq oveq2 subid1i
      abs0 addid2 simprd eqbrtrd adantr wne absmul simp2d absid eqbrtrrd leltne
      eqtrd biimpar ltmul2 syl112anc simp3d abssubge0 subge0 mpbird jca wi ltle
      mpd lemul2a syl31anc remulcl ltleadd breqtrd pm2.61dane lelttrd mpbir2and
      mp2and syl6eleqr ) DLMZEUAMZUHZAFMZBFMZGNOUBPMZUCZUHZGAQPZOGUDPZBQPZUEPZD
      ECUFUGPZFUUEUUIUUJMZUUILMZDUUICPZERUIZUUEUUFLMZUUHLMZUULUUEGLMZALMZUUOUUE
      GUUEGUJMZNGUKUIZGOUKUIZUUEUUCUUSUUTUVAUCYTUUAUUBUUCULNOGUMVAUNUOZUPZUQZUU
      EUURDACPZERUIZUUEAUUJMZUURUVFUHZUUEAFUUJYTUUAUUBUUCURKUSUUECVBMZYRYSUVGUV
      HWCUVIUUECIUTVCZYRYSUUDVDZYRYSUUDVEZACDELCIVFZVGVHVIZVJZGAVKSZUUEUUGLMZBL
      MZUUPUUEUUGUUEOUJMZUUSUUGUJMZVAUVCOGVLVMZUQZUUEUVRDBCPZERUIZUUEBUUJMZUVRU
      WDUHZUUEBFUUJYTUUAUUBUUCVNKUSUUEUVIYRYSUWEUWFWCUVJUVKUVLBCDELUVMVGVHVIZVJ
      ZUUGBVKSZUUFUUHVOSZUUEUUMGDAUDPZQPZUUGDBUDPZQPZUEPZVPUGZERUUEUUMDUUIUDPZV
      PUGZUWPUUEYRUULUUMUWRTUVKUWJDUUICIVQSUUEUWOUWQVPUUEUWOGDQPZUUFUDPZUUGDQPZ
      UUHUDPZUEPZUWSUXAUEPZUUIUDPZUWQUUEUWLUWTUWNUXBUEUUEUUQYRUURUWLUWTTUVDUVKU
      VOGDAVRVHUUEUVQYRUVRUWNUXBTUWBUVKUWHUUGDBVRVHVSUUEUWSLMZUXALMZUUOUUPUXEUX
      CTUUEUUQYRUXFUVDUVKGDVKSUUEUVQYRUXGUWBUVKUUGDVKSUVPUWIUWSUXAUUFUUHVTWAUUE
      UXDDUUIUDUUEGUUGUEPZDQPZODQPZUXDDUUEUXHODQUUEUUQOLMUXHOTUVDWBGOWDWEZWFUUE
      UUQUVQYRUXIUXDTUVDUWBUVKGUUGDWGVHUUEYRUXJDTUVKDWHWNWIWFWJWKWOUUEUWPUWLVPU
      GZUWNVPUGZUEPZEUUEUWOLMZUWPUJMUUEUWLLMZUWNLMZUXOUUEUUQUWKLMZUXPUVDUUEYRUU
      RUXRUVKUVODAWLSZGUWKVKSZUUEUVQUWMLMZUXQUWBUUEYRUVRUYAUVKUWHDBWLSZUUGUWMVK
      SZUWLUWNVOSUWOWMWNUUEUXLUJMZUXMUJMZUXNUJMUUEUXPUYDUXTUWLWMWNZUUEUXQUYEUYC
      UWNWMWNZUXLUXMWPSUUEYSEUJMZUVLEWQWNZUUEUXPUXQUWPUXNUKUIUXTUYCUWLUWNWRSUUE
      UXNERUIGNUUEGNTZUHZUXNNUWMVPUGZUEPZERUYKUXLNUXMUYLUEUYKUXLNVPUGNUYKUWLNVP
      UYJUUEUWLNUWKQPZNGNUWKQWSUUEUXRUYNNTUXSUWKWTWNXAWKXEXBUYKUWNUWMVPUYJUUEUW
      NOUWMQPZUWMUYJUUGOUWMQUYJUUGONUDPOGNOUDXCOWBXDXBWFUUEUYAUYOUWMTUYBUWMWHWN
      XAWKVSUUEUYMERUIUYJUUEUYMUWCERUUEUYMUYLUWCUUEUYLLMUYMUYLTUUEUYLUUEUYAUYLU
      JMZUYBUWMWMWNZUQUYLXFWNUUEYRUVRUWCUYLTUVKUWHDBCIVQSZWOUUEUVRUWDUWGXGZXHXI
      XHUUEGNXJZUHZUXNGEQPZUUGEQPZUEPZERVUAUXLVUBRUIZUXMVUCUKUIZUXNVUDRUIZVUAUX
      LGUWKVPUGZQPZVUBRUUEUXLVUITUYTUUEUXLGVPUGZVUHQPZVUIUUEUUQUXRUXLVUKTUVDUXS
      GUWKXKSUUEVUJGVUHQUUEUUSUUTVUJGTUVCUUEUUSUUTUVAUVBXLZGXMSWFXPXIVUAVUHERUI
      ZVUIVUBRUIZUUEVUMUYTUUEUVEVUHERUUEYRUURUVEVUHTUVKUVODACIVQSUUEUURUVFUVNXG
      XNXIVUAVUHUJMZUYHUUSNGRUIZVUMVUNWCUUEVUOUYTUUEUXRVUOUXSUWKWMWNXIUUEUYHUYT
      UYIXIUUEUUSUYTUVCXIUUEVUPUYTUUENUJMZUUSUUTVUPUYTWCVUQUUEUMVCUVCVULNGXOVHX
      QVUHEGXRXSVIXHUUEVUFUYTUUEUXMUUGUYLQPZVUCUKUUEUXMUUGVPUGZUYLQPZVURUUEUVQU
      YAUXMVUTTUWBUYBUUGUWMXKSUUEVUSUUGUYLQUUEUUSUVSUVAVUSUUGTUVCUVSUUEVAVCZUUE
      UUSUUTUVAUVBXTZGOYAVHWFXPUUEUYPUYHUVTNUUGUKUIZUHUYLEUKUIZVURVUCUKUIUYQUYI
      UUEUVTVVCUWAUUEVVCUVAVVBUUEUVSUUSVVCUVAWCVVAUVCOGYBSYCYDUUEUYLERUIZVVDUUE
      UWCUYLERUYRUYSXNUUEUYPUYHVVEVVDYEUYQUYIUYLEYFSYGUYLEUUGYHYIXHXIUUEVUEVUFU
      HVUGYEZUYTUUEUYDUYEVUBUJMZVUCUJMZVVFUYFUYGUUEUUSUYHVVGUVCUYIGEYJSUUEUVTUY
      HVVHUWAUYIUUGEYJSUXLUXMVUBVUCYKWAXIYPUUEVUDETUYTUUEUXHEQPZOEQPZVUDEUUEUXH
      OEQUXKWFUUEUUQUVQELMZVVIVUDTUVDUWBUUEEUYIUQZGUUGEWGVHUUEVVKVVJETVVLEWHWNW
      IXIYLYMYNXHUUEUVIYRYSUUKUULUUNUHWCUVJUVKUVLUUICDELUVMVGVHYOKYQ $.
      $( [12-Feb-2015] $)

    $d t J $.  $d t x y K $.  $d t x y P $.  $d t x y R $.  $d t x y S $.
    blscon.k $e |- K = ( subSp ` <. S , J >. ) $.
    $( An open ball in the complex numbers is simply connected.  (Contributed
       by Mario Carneiro, 12-Feb-2015.) $)
    blscon $p |- ( ( P e. CC /\ R e. RR+ ) -> K e. SCon ) $=
      ( vx vy vt cc wcel crp wa cfv cv copn cbl co cme wss cnmet cnmetba mp3an1
      blssm syl5eqss blcvx cabs cmin ccom fveq2i eqtri cvxscon ) BNOZCPOZQZKLMD
      EFUSDBCAUARUBZNIAUCOUQURUTNUDAGUEABCNAGUFUHUGUIKSLSABCDMSEGHIUJEATRUKULUM
      ZTRHAVATGUNUOJUP $.
      $( [12-Feb-2015] $)
  $}

