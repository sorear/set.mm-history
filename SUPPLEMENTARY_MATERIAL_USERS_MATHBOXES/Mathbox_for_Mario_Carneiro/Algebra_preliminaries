$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Algebra preliminaries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Irred $. $( Ring irreducible $)
  $c RingHom $. $( Ring homomorphism $)
  $c RingIso $. $( Ring isomorphism $)
  $c ~QG $. $( Quotient group equivalence $)
  $c "s $. $( Image structure $)
  $c /s $. $( Quotient structure $)
  $c X.s $. $( Product structure $)
  $c RSpan $. $( Ring span function $)
  $c AssAlg $. $( Associative algebra $)
  $c AlgSpan $. $( Algebraic span $)

  $( Ring irreducibles. $)
  cir $a class Irred $.

  $( Ring homomorphisms. $)
  crh $a class RingHom $.

  $( Ring isomorphisms. $)
  crs $a class RingIso $.

  $( Quotient group equivalence class. $)
  cqg $a class ~QG $.

  $( Image structure function. $)
  cimas $a class "s $.

  $( Quotient structure function. $)
  cqus $a class /s $.

  $( Product structure function. $)
  cprs $a class X.s $.

  $( Ring span function. $)
  crsp $a class RSpan $.

  $( Associative algebra. $)
  casa $a class AssAlg $.

  $( Algebraic span function. $)
  casp $a class AlgSpan $.

  ${
    $d b e f g h i n p q r s t v w x y z $.
    $( Define the set of irreducible elements in a ring. $)
    df-irred $a |- Irred = ( w e. _V |->
      [_ ( ( Base ` w ) \ ( Unit ` w ) ) / b ]_
        { z e. b | A. x e. b A. y e. b ( x ( .r ` w ) y ) =/= z } ) $.

    $( Define the set of ring homomorphisms from ` r ` to ` s ` . $)
    df-rnghom $a |- RingHom = ( r e. _V , s e. _V |->
      [_ ( Base ` r ) / v ]_ [_ ( Base ` s ) / w ]_
   { f e. ( w ^m v ) | ( ( f ` ( 1r ` r ) ) = ( 1r ` s ) /\ A. x e. v A. y e. v
   ( ( f ` ( x ( +g ` r ) y ) ) = ( ( f ` x ) ( +g ` s ) ( f ` y ) ) /\
     ( f ` ( x ( .r ` r ) y ) ) = ( ( f ` x ) ( .r ` s ) ( f ` y ) ) ) ) } ) $.

    $( Define the set of ring isomorphisms from ` r ` to ` s ` . $)
    df-rngiso $a |- RingIso = ( r e. _V , s e. _V |->
      { f e. ( r RingHom s ) | `' f e. ( s RingHom r ) } ) $.

    $( Define the equivalence relation in a quotient ring or quotient group
       (where ` i ` is a two-sided ideal or a normal subgroup). $)
    df-eqg $a |- ~QG = ( r e. _V , i e. ~P ( Base ` r ) |-> { <. x , y >. |
      ( { x , y } C_ ( Base ` r ) /\ ( x ( -g ` r ) y ) e. i ) } ) $.

    $( Define an image structure, which takes a structure and a function on the
       base set, and maps all the operations via the function.  For this to
       work properly ` f ` must either be injective or satisfy the
       well-definedness condition ` f ( a ) = f ( c ) /\ f ( b ) = f ( d ) -> `
       ` f ( a + c ) = f ( b + d ) ` for each relevant operation. $)
    df-imas $a |- "s = ( f e. _V , r e. _V |-> [_ ( Base ` r ) / v ]_
    ( { <. ( Base ` ndx ) , ran f >. ,
        <. ( +g ` ndx ) , U_ p e. v U_ q e. v
     { <. <. ( f ` p ) , ( f ` q ) >. , ( f ` ( p ( +g ` r ) q ) ) >. } >. ,
        <. ( .r ` ndx ) , U_ p e. v U_ q e. v
     { <. <. ( f ` p ) , ( f ` q ) >. , ( f ` ( p ( .r ` r ) q ) ) >. } >. } u.
      { <. ( TopOpen ` ndx ) , { s e. ~P ran f |
           ( `' f " s ) e. ( TopOpen ` r ) } >. ,
        <. ( dist ` ndx ) , ( x e. ran f , y e. ran f |-> sup ( U_ n e. NN
         ran ( g e. { h e. ( ( dom f X. dom f ) ^m ( 1 ... n ) ) |
         ( ( 1st ` ( h ` 1 ) ) = x /\ ( 2nd ` ( h ` n ) ) = y /\
         A. i e. ( 1 ... ( n - 1 ) ) ( f ` ( 2nd ` ( h ` i ) ) ) =
           ( f ` ( 1st ` ( h ` ( i + 1 ) ) ) ) ) } |-> sum_ i e. dom g
           ( ( dist ` r ) ` ( g ` i ) ) ) , RR , `' < ) ) >. } ) ) $.

    $( Define a quotient ring (or quotient group), which is a special case of
       an image structure ~ df-imas where the image function is
       ` x |-> [ x ] e ` . $)
    df-divs $a |- /s = ( r e. _V , e e. _V |->
      ( ( x e. ( Base ` r ) |-> [ x ] e ) "s r ) ) $.

    $( Define a structure product.  In usual usage ` r ` will be a function
       from some index set to a structure like ` Ring ` or ` TopSp ` . $)
    df-prd $a |- X.s = ( r e. _V |->
      [_ X_ x e. dom r ( Base ` ( r ` x ) ) / v ]_
    ( { <. ( Base ` ndx ) , v >. ,
        <. ( +g ` ndx ) , ( f e. v , g e. v |-> ( x e. dom r |->
          ( ( f ` x ) ( +g ` ( r ` x ) ) ( g ` x ) ) ) ) >. ,
        <. ( .r ` ndx ) , ( f e. v , g e. v |-> ( x e. dom r |->
          ( ( f ` x ) ( .r ` ( r ` x ) ) ( g ` x ) ) ) ) >. } u.
      { <. ( TopOpen ` ndx ) , ( tP ` ( TopOpen o. r ) ) >. ,
        <. ( dist ` ndx ) , ( f e. v , g e. v |-> sup ( ran ( x e. dom r |->
       ( ( f ` x ) ( dist ` ( r ` x ) ) ( g ` x ) ) ) , RR* , < ) ) >. } ) ) $.

    $( Define the linear span function in a ring. $)
    df-rsp $a |- RSpan = ( LSpan o. ringLMod ) $.

    $( A (unital) associative algebra is a vector space that also contains a
       multiplication operation on the vectors, which is bilinear under the
       scalar product. $)
    df-assa $a |- AssAlg = { w e. ( LMod i^i Ring ) |
    A. r e. ( Base ` ( Scalar ` w ) ) A. x e. ( Base ` w ) A. y e. ( Base ` w )
      [ ( vsca ` w ) / s ] [ ( .r ` w ) / t ]
      { ( r s ( x t y ) ) , ( ( r s x ) t y ) , ( x t ( r s y ) ) } ~~ 1o } $.

    $( Define the algebraic span of a set of vectors in an algebra. $)
    df-algsp $a |- AlgSpan = ( w e. AssAlg |-> ( s e. ~P ( Base ` w ) |->
      |^| { t e. ( SubRing ` w ) | ( ( w |`s s ) e. AssAlg /\ s C_ t ) } ) ) $.
  $}

  ${
    $d b r x y z N $.  $d b r x y z R $.  $d b r z T $.  $d x y z X $.
    irred.1 $e |- B = ( Base ` R ) $.
    irred.2 $e |- U = ( Unit ` R ) $.
    irred.3 $e |- I = ( Irred ` R ) $.
    irred.4 $e |- N = ( B \ U ) $.
    irred.5 $e |- T = ( .r ` R ) $.
    $( Define an irreducible element of a ring, which is a non-unit that is not
       the product of two non-units.  (Contributed by Mario Carneiro,
       2-Dec-2014.) $)
    irredval $p |- ( R e. V -> I =
      { z e. N | A. x e. N A. y e. N ( x T y ) =/= z } ) $=
      ( wcel cvv cv cfv cbs vr vb co wne wral crab wceq elex cir cui cdif cmulr
      csb fvex difexg ax-mp a1i simpr simpl fveq2d syl6eqr difeq12d eqtrd oveqd
      neeq1d raleqbidv rabeqbidv csbied df-irred eqeltri rabex fvmpt syl5eq syl
      wa ) EJPEQPZHARZBRZFUCZCRZUDZBIUEZAIUEZCIUFZUGEJUHVPHEUISWDMUAEUBUARZTSZW
      EUJSZUKZVQVRWEULSZUCZVTUDZBUBRZUEZAWLUEZCWLUFZUMWDQUIWEEUGZUBWHWOWDQWHQPZ
      WPWFQPWQWETUNWFWGQUOUPUQWPWLWHUGZVOZWNWCCWLIWSWLWHIWPWRURWSWHDGUKZIWSWFDW
      GGWSWFETSZDWSWEETWPWRUSZUTKVAWSWGEUJSGWSWEEUJXBUTLVAVBNVAVCZWSWMWBAWLIXCW
      SWKWABWLIXCWSWJVSVTWSWIFVQVRWSWIEULSFWSWEEULXBUTOVAVDVEVFVFVGVHABCUAUBVIW
      CCIIWTQNDQPWTQPDXAQKETUNVJDGQUOUPVJVKVLVMVN $.
      $( [2-Dec-2014] $)

    $( Value of the divides relation. $)
    isirred $p |- ( R e. V -> ( X e. I <-> ( X e. N /\
      A. x e. N A. y e. N ( x T y ) =/= X ) ) ) $=
      ( vz wcel cv wne wral co crab irredval eleq2d neeq2 2ralbidv elrab syl6bb
      wa wceq ) DIQZJGQJARBREUAZPRZSZBHTAHTZPHUBZQJHQULJSZBHTAHTZUIUKGUPJABPCDE
      FGHIKLMNOUCUDUOURPJHUMJUJUNUQABHHUMJULUEUFUGUH $.
      $( [2-Dec-2014] $)
  $}

