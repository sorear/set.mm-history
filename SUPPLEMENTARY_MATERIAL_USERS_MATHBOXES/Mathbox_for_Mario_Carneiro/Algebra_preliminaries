$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Algebra preliminaries
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Irred $. $( Ring irreducible $)
  $c RingHom $. $( Ring homomorphism $)
  $c RingIso $. $( Ring isomorphism $)
  $c "s $. $( Image structure $)
  $c /s $. $( Quotient structure $)
  $c Xs. $. $( Binary product structure $)
  $c AssAlg $. $( Associative algebra $)
  $c AlgSpan $. $( Algebraic span $)

  $( Ring irreducibles. $)
  cir $a class Irred $.

  $( Ring homomorphisms. $)
  crh $a class RingHom $.

  $( Ring isomorphisms. $)
  crs $a class RingIso $.

  $( Image structure function. $)
  cimas $a class "s $.

  $( Quotient structure function. $)
  cqus $a class /s $.

  $( Binary product structure function. $)
  cxps $a class Xs. $.

  $( Associative algebra. $)
  casa $a class AssAlg $.

  $( Algebraic span function. $)
  casp $a class AlgSpan $.

  ${
    $d b e f g h i n p q r s t v w x y z $.
    $( Define the set of irreducible elements in a ring. $)
    df-irred $a |- Irred = ( w e. _V |->
      [_ ( ( Base ` w ) \ ( Unit ` w ) ) / b ]_
        { z e. b | A. x e. b A. y e. b ( x ( .r ` w ) y ) =/= z } ) $.

    $( Define the set of ring homomorphisms from ` r ` to ` s ` . $)
    df-rnghom $a |- RingHom = ( r e. _V , s e. _V |->
      [_ ( Base ` r ) / v ]_ [_ ( Base ` s ) / w ]_
   { f e. ( w ^m v ) | ( ( f ` ( 1r ` r ) ) = ( 1r ` s ) /\ A. x e. v A. y e. v
   ( ( f ` ( x ( +g ` r ) y ) ) = ( ( f ` x ) ( +g ` s ) ( f ` y ) ) /\
     ( f ` ( x ( .r ` r ) y ) ) = ( ( f ` x ) ( .r ` s ) ( f ` y ) ) ) ) } ) $.

    $( Define the set of ring isomorphisms from ` r ` to ` s ` . $)
    df-rngiso $a |- RingIso = ( r e. _V , s e. _V |->
      { f e. ( r RingHom s ) | `' f e. ( s RingHom r ) } ) $.

    $( Define an image structure, which takes a structure and a function on the
       base set, and maps all the operations via the function.  For this to
       work properly ` f ` must either be injective or satisfy the
       well-definedness condition ` f ( a ) = f ( c ) /\ f ( b ) = f ( d ) -> `
       ` f ( a + c ) = f ( b + d ) ` for each relevant operation. $)
    df-imas $a |- "s = ( f e. _V , r e. _V |-> [_ ( Base ` r ) / v ]_
  ( ( { <. ( Base ` ndx ) , ran f >. ,
        <. ( +g ` ndx ) , U_ p e. v U_ q e. v
     { <. <. ( f ` p ) , ( f ` q ) >. , ( f ` ( p ( +g ` r ) q ) ) >. } >. ,
        <. ( .r ` ndx ) , U_ p e. v U_ q e. v
     { <. <. ( f ` p ) , ( f ` q ) >. , ( f ` ( p ( .r ` r ) q ) ) >. } >. } u.
      { <. ( Scalar ` ndx ) , ( Scalar ` r ) >. ,
        <. ( vsca ` ndx ) , U_ q e. v ( p e. ( Scalar ` r ) ,
           x e. { ( f ` q ) } |-> ( f ` ( p ( vsca ` r ) q ) ) ) >. ,
        <. ( TopOpen ` ndx ) , { s e. ~P ran f |
           ( `' f " s ) e. ( TopOpen ` r ) } >. } ) u.
      { <. ( dist ` ndx ) , ( x e. ran f , y e. ran f |-> sup ( U_ n e. NN
         ran ( g e. { h e. ( ( dom f X. dom f ) ^m ( 1 ... n ) ) |
         ( ( 1st ` ( h ` 1 ) ) = x /\ ( 2nd ` ( h ` n ) ) = y /\
         A. i e. ( 1 ... ( n - 1 ) ) ( f ` ( 2nd ` ( h ` i ) ) ) =
           ( f ` ( 1st ` ( h ` ( i + 1 ) ) ) ) ) } |-> sum_ i e. dom g
           ( ( dist ` r ) ` ( g ` i ) ) ) , RR , `' < ) ) >. ,
        <. ( le ` ndx ) , ( ( f o. ( le ` r ) ) o. `' f ) >. } ) ) $.

    $( Define a quotient ring (or quotient group), which is a special case of
       an image structure ~ df-imas where the image function is
       ` x |-> [ x ] e ` . $)
    df-divs $a |- /s = ( r e. _V , e e. _V |->
      ( ( x e. ( Base ` r ) |-> [ x ] e ) "s r ) ) $.

    $( Define a binary product on structures. $)
    df-xps $a |- Xs. = ( r e. _V , s e. _V |->
        ( `' ( x e. ( Base ` r ) , y e. ( Base ` s ) |->
      { <. (/) , x >. , <. 1o , y >. } ) "s ( Xm_ ` ( { r } +c { s } ) ) ) ) $.

    $( Definition of an associative algebra.  An associative algebra is a set
       equipped with a left-module structure on a (commutative) ring, coupled
       with an multiplicative internal operation on the vectors of the module
       that is associative and distributive for the additive structure of the
       left-module (so giving the vectors a ring structure) and that is also
       bilinear under the scalar product. $)
    df-assa $a |- AssAlg = { w e. ( LMod i^i Ring ) | [ ( Scalar ` w ) / f ]
      ( f e. CRing /\ A. r e. ( Base ` f ) A. x e. ( Base ` w )
      A. y e. ( Base ` w ) [ ( vsca ` w ) / s ] [ ( .r ` w ) / t ]
      ( ( ( r s x ) t y ) = ( r s ( x t y ) ) /\
        ( x t ( r s y ) ) = ( r s ( x t y ) ) ) ) } $.

    $( Define the algebraic span of a set of vectors in an algebra. $)
    df-asp $a |- AlgSpan = ( w e. AssAlg |-> ( s e. ~P ( Base ` w ) |->
      |^| { t e. ( ( SubRing ` w ) i^i ( LSubSp ` w ) ) | s C_ t } ) ) $.
  $}

  ${
    $d b r x y z N $.  $d b r x y z R $.  $d b r z T $.  $d x y z X $.
    irred.1 $e |- B = ( Base ` R ) $.
    irred.2 $e |- U = ( Unit ` R ) $.
    irred.3 $e |- I = ( Irred ` R ) $.
    irred.4 $e |- N = ( B \ U ) $.
    irred.5 $e |- T = ( .r ` R ) $.
    $( An irreducible element of a ring is a non-unit that is not the product
       of two non-units.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    isirred $p |- ( X e. I <-> ( X e. N /\
      A. x e. N A. y e. N ( x T y ) =/= X ) ) $=
      ( vz wcel cvv wral cfv cbs vr vb cv co wne cir cdm elfvdm eleq2s elex syl
      wa cdif eldifi syl6eleq 3syl adantr crab cui cmulr wceq fvex difexg ax-mp
      csb a1i simpr simpl fveq2d syl6eqr eqtrd oveqd neeq1d raleqbidv rabeqbidv
      difeq12d csbied df-irred eqeltri rabex fvmpt syl5eq eleq2d neeq2 2ralbidv
      elrab syl6bb pm5.21nii ) IGPZDQPZIHPZAUCZBUCZEUDZIUEZBHRAHRZULZWIDUFUGZPZ
      WJWSIDUFSZGIDUFUHLUIDWRUJUKWKWJWPWKIDTSZPDTUGZPWJWKICXAICPICFUMZHICFUNMUI
      JUOIDTUHDXBUJUPUQWJWIIWNOUCZUEZBHRZAHRZOHURZPWQWJGXHIWJGWTXHLUADUBUAUCZTS
      ZXIUSSZUMZWLWMXIUTSZUDZXDUEZBUBUCZRZAXPRZOXPURZVEXHQUFXIDVAZUBXLXSXHQXLQP
      ZXTXJQPYAXITVBXJXKQVCVDVFXTXPXLVAZULZXRXGOXPHYCXPXLHXTYBVGYCXLXCHYCXJCXKF
      YCXJXACYCXIDTXTYBVHZVIJVJYCXKDUSSFYCXIDUSYDVIKVJVPMVJVKZYCXQXFAXPHYEYCXOX
      EBXPHYEYCXNWNXDYCXMEWLWMYCXMDUTSEYCXIDUTYDVINVJVLVMVNVNVOVQABOUAUBVRXGOHH
      XCQMCQPXCQPCXAQJDTVBVSCFQVCVDVSVTWAWBWCXGWPOIHXDIVAXEWOABHHXDIWNWDWEWFWGW
      H $.
      $( [2-Dec-2014] $)

    $( The property of being a non-irreducible (reducible) element in a ring.
       (Contributed by Mario Carneiro, 4-Dec-2014.) $)
    isnirred $p |- ( X e. B -> ( -. X e. I <-> ( X e. U \/
      E. x e. N E. y e. N ( x T y ) = X ) ) ) $=
      ( wcel cv wrex wn wa wral co wceq ioran wne cdif eleq2i eldif bitri baibr
      wo df-ne ralbii ralnex bitr2i a1i anbi12d isirred syl6bbr syl5bb con1bid
      wb ) ICOZIFOZAPBPEUAZIUBZBHQZAHQZUJZIGOZVHRVCRZVGRZSZVBVIVCVGUCVBVLIHOZVD
      IUDZBHTZAHTZSVIVBVJVMVKVPVMVBVJVMICFUEZOVBVJSHVQIMUFICFUGUHUIVKVPVAVBVPVF
      RZAHTVKVOVRAHVOVERZBHTVRVNVSBHVDIUKULVEBHUMUHULVFAHUMUNUOUPABCDEFGHIJKLMN
      UQURUSUT $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x y B $.  $d x y R $.  $d x y U $.  $d x y X $.
    isirred2.1 $e |- B = ( Base ` R ) $.
    isirred2.2 $e |- U = ( Unit ` R ) $.
    isirred2.3 $e |- I = ( Irred ` R ) $.
    isirred2.4 $e |- T = ( .r ` R ) $.
    $( Expand out the set differences from ~ isirred .  (Contributed by Mario
       Carneiro, 4-Dec-2014.) $)
    isirred2 $p |- ( X e. I <-> ( X e. B /\ -. X e. U /\
      A. x e. B A. y e. B ( ( x T y ) = X -> ( x e. U \/ y e. U ) ) ) ) $=
      ( wcel cv wral wa wn wi eldif wal cdif co wne wceq w3a anbi12i an4 imbi1i
      bitri impexp pm4.56 df-ne imbi12i con34b bitr4i imbi2i 3bitri 2albii r2al
      wo 3bitr4i eqid isirred df-3an ) HCFUAZMZANZBNZEUBZHUCZBVEOAVEOZPHCMZHFMQ
      ZPZVIHUDZVGFMZVHFMZUTZRZBCOACOZPHGMVLVMVTUEVFVNVKVTHCFSVGVEMZVHVEMZPZVJRZ
      BTATVGCMZVHCMZPZVSRZBTATVKVTWDWHABWDWGVPQZVQQZPZPZVJRWGWKVJRZRWHWCWLVJWCW
      EWIPZWFWJPZPWLWAWNWBWOVGCFSVHCFSUFWEWIWFWJUGUIUHWGWKVJUJWMVSWGWMVRQZVOQZR
      VSWKWPVJWQVPVQUKVIHULUMVOVRUNUOUPUQURVJABVEVEUSVSABCCUSVAUFABCDEFGVEHIJKV
      EVBLVCVLVMVTVDVA $.
      $( [4-Dec-2014] $)
  $}

  ${
    $d x I $.  $d y z R $.  $d x y z S $.
    opprirred.1 $e |- S = ( oppR ` R ) $.
    opprirred.2 $e |- I = ( Irred ` R ) $.
    $( Irreducibility is symmetric, so the irreducible elements of the opposite
       ring are the same as the original ring.  (Contributed by Mario Carneiro,
       4-Dec-2014.) $)
    opprirred $p |- I = ( Irred ` S ) $=
      ( vx vz vy cir cfv cv wcel cmulr co wne wral wa eldifi eqid isirred bitri
      cbs cui cdif ralcom opprmul neeq1d syl2an ralbidva ralbiia anbi2i opprbas
      wb opprunit 3bitr4i eqriv ) FCBIJZFKZAUBJZAUCJZUDZLZGKZHKZAMJZNZUROZHVAPG
      VAPZQVBVDVCBMJZNZUROZGVAPZHVAPZQURCLURUQLVHVMVBVHVGGVAPZHVAPVMVGGHVAVAUEV
      NVLHVAVDVALZVGVKGVAVOVDUSLZVCUSLZVGVKUMVCVALVDUSUTRVCUSUTRVPVQQVFVJURUSAV
      EVIBVDVCUSSZVESZDVISZUFUGUHUIUJUAUKGHUSAVEUTCVAURVRUTSZEVASZVSTHGUSBVIUTU
      QVAURUSABDVRULABUTWADUNUQSWBVTTUOUP $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d x y z R $.  $d x y z T $.  $d x y z U $.  $d x y z X $.  $d x y z Y $.
    $d x y B $.  $d x y I $.  $d x y Z $.
    irredn0.i $e |- I = ( Irred ` R ) $.
    ${
      irredn0.z $e |- Z = ( 0g ` R ) $.
      $( The additive identity is not irreducible.  (Contributed by Mario
         Carneiro, 4-Dec-2014.) $)
      irredn0 $p |- ( ( R e. Ring /\ X e. I ) -> X =/= Z ) $=
        ( vx vy crg wcel wa wn cfv cv co wceq wrex eqid adantr eqeq1d wne cmulr
        cui cdif wo rng0cl anim1i eldif sylibr rnglz mpdan oveq1 oveq2 rcla42ev
        cbs syl3anc ex orrd wb isnirred syl mpbird simpr syl5ibcom necon3bd mpd
        eleq1 ) AIJZCBJZKZDBJZLZCDUAVHVLVIVHVLDAUCMZJZGNZHNZAUBMZOZDPZHAUOMZVMU
        DZQGWAQZUEZVHVNWBVHVNLZWBVHWDKZDWAJZWFDDVQOZDPZWBWEDVTJZWDKWFVHWIWDVTAD
        VTRZFUFZUGDVTVMUHUIZWLVHWHWDVHWIWHWKVTAVQDDWJVQRZFUJUKSVSWHDVPVQOZDPGHD
        DWAWAVODPVRWNDVODVPVQULTVPDPWNWGDVPDDVQUMTUNUPUQURVHWIVLWCUSWKGHVTAVQVM
        BWADWJVMREWARWMUTVAVBSVJVKCDVJVICDPVKVHVIVCCDBVGVDVEVF $.
        $( [4-Dec-2014] $)
    $}

    ${
      irredcl.b $e |- B = ( Base ` R ) $.
      $( An irreducible element is in the ring.  (Contributed by Mario
         Carneiro, 4-Dec-2014.) $)
      irredcl $p |- ( X e. I -> X e. B ) $=
        ( vx vy wcel cui cfv wn cv cmulr co wceq wo wi wral eqid isirred2
        simp1bi ) DCIDAIDBJKZILGMZHMZBNKZODPUDUCIUEUCIQRHASGASGHABUFUCCDFUCTEUF
        TUAUB $.
        $( [4-Dec-2014] $)
    $}

    ${
      irrednu.u $e |- U = ( Unit ` R ) $.
      $( An irreducible element is not a unit.  (Contributed by Mario Carneiro,
         4-Dec-2014.) $)
      irrednu $p |- ( X e. I -> -. X e. U ) $=
        ( vx vy wcel cbs cfv wn cv cmulr co wceq wo wi wral eqid isirred2
        simp2bi ) DCIDAJKZIDBILGMZHMZANKZODPUDBIUEBIQRHUCSGUCSGHUCAUFBCDUCTFEUF
        TUAUB $.
        $( [4-Dec-2014] $)
    $}

    ${
      irredn1.o $e |- O = ( 1r ` R ) $.
      $( The multiplicative identity is not irreducible.  (Contributed by Mario
         Carneiro, 4-Dec-2014.) $)
      irredn1 $p |- ( ( R e. Ring /\ X e. I ) -> X =/= O ) $=
        ( crg wcel wne cui wn eqid irrednu wceq 1unit eleq1 syl5ibrcom necon3bd
        cfv syl5 imp ) AGHZDBHZDCIZUCDAJSZHZKUBUDAUEBDEUELZMUBUFDCUBUFDCNCUEHAU
        ECUGFODCUEPQRTUA $.
        $( [4-Dec-2014] $)
    $}

    ${
      irredrmul.u $e |- U = ( Unit ` R ) $.
      irredrmul.t $e |- T = ( .r ` R ) $.
      $( The product of an irreducible element and a unit is irreducible.
         (Contributed by Mario Carneiro, 4-Dec-2014.) $)
      irredrmul $p |- ( ( R e. Ring /\ X e. I /\ Y e. U ) ->
        ( X T Y ) e. I ) $=
        ( vx vy vz wcel co cv wceq wrex wn eqid syl3anc crg w3a cbs cfv cdif wo
        simp2 cdvr simp1 simp3 unitdvcl 3com23 3expia syl2anc irredcl rngdvcan3
        wi 3ad2ant2 eleq1d sylibd wa ad2antrr eldifi ad2antrl rngdvcl unitmulcl
        eldifn rngdvcan1 mtod sylanbrc simprr oveq1d ad2antlr rngdvass syl13anc
        eldif 3eqtr3d oveq2 eqeq1d rcla4ev rexlimdva reximdva orim12d wb unitcl
        expr 3ad2ant3 rngcl isnirred syl 3imtr4d mt4d ) AUAMZEDMZFCMZUBZWNEFBNZ
        DMZWMWNWOUGWPWQCMZJOZKOZBNZWQPZKAUCUDZCUEZQZJXEQZUFZECMZWTLOZBNZEPZLXEQ
        ZJXEQZUFZWRRZWNRZWPWSXIXGXNWPWSWQFAUHUDZNZCMZXIWPWMWOWSXTUQWMWNWOUIZWMW
        NWOUJZWMWOWSXTWMWSWOXTXDXRACWQFXDSZHXRSZUKULUMUNWPXSECWPWMEXDMZWOXSEPZY
        AWNWMYEWOXDADEGYCUOURZYBXDXRABCEFYCHYDIUPTZUSUTWPXFXMJXEWPWTXEMZVAZXCXM
        KXEYJXAXEMZXCXMYJYKXCVAZVAZXAFXRNZXEMZWTYNBNZEPZXMYMYNXDMZYNCMZRYOYMWMX
        AXDMZWOYRWPWMYIYLYAVBZYKYTYJXCXAXDCVCVDZWPWOYIYLYBVBZXDXRACXAFYCHYDVETY
        MYSXACMZYKUUDRYJXCXAXDCVGVDYMYSYNFBNZCMZUUDYMWMWOYSUUFUQUUAUUCWMWOYSUUF
        WMYSWOUUFABCYNFHIVFULUMUNYMUUEXACYMWMYTWOUUEXAPUUAUUBUUCXDXRABCXAFYCHYD
        IVHTUSUTVIYNXDCVPVJYMXBFXRNZXSYPEYMXBWQFXRYJYKXCVKVLYMWMWTXDMZYTWOUUGYP
        PUUAYIUUHWPYLWTXDCVCVMUUBUUCXDXRABCWTXAFYCHYDIVNVOWPYFYIYLYHVBVQXLYQLYN
        XEXJYNPXKYPEXJYNWTBVRVSVTUNWFWAWBWCWPWQXDMZXPXHWDWPWMYEFXDMZUUIYAYGWOWM
        UUJWNXDACFYCHWEWGXDABEFYCIWHTJKXDABCDXEWQYCHGXESZIWIWJWPYEXQXOWDYGJLXDA
        BCDXEEYCHGUUKIWIWJWKWL $.
        $( [4-Dec-2014] $)

      $( The product of a unit and an irreducible element is irreducible.
         (Contributed by Mario Carneiro, 4-Dec-2014.) $)
      irredlmul $p |- ( ( R e. Ring /\ X e. U /\ Y e. I ) ->
        ( X T Y ) e. I ) $=
        ( crg wcel w3a co coppr cfv cmulr wceq cbs eqid irredcl opprmul syl2anr
        unitcl 3adant1 opprrng opprirred opprunit irredrmul syl3an1 eqeltrd
        3com23 ) AJKZECKZFDKZLEFBMZFEANOZPOZMZDUMUNUOURQZULUNFAROZKEUTKUSUMUTAD
        FGUTSZTUTACEVAHUCUTABUQUPFEVAIUPSZUQSZUAUBUDULUNUMURDKZULUPJKUNUMVDAUPV
        BUEUPUQCDFEAUPDVBGUFAUPCHVBUGVCUHUIUKUJ $.
        $( [4-Dec-2014] $)
    $}

    ${
      irredmul.b $e |- B = ( Base ` R ) $.
      irredmul.u $e |- U = ( Unit ` R ) $.
      irredmul.t $e |- T = ( .r ` R ) $.
      $( If product of two elements is irreducible, then one of the elements
         must be a unit.  (Contributed by Mario Carneiro, 4-Dec-2014.) $)
      irredmul $p |- ( ( X e. B /\ Y e. B /\ ( X T Y ) e. I ) ->
        ( X e. U \/ Y e. U ) ) $=
        ( vx vy wcel co wo cv wceq wi wral wa isirred2 simp3bi eqid oveq1 eleq1
        wn eqeq1d orbi1d imbi12d oveq2 orbi2d rcla42v mpii syl5 3impia ) FANZGA
        NZFGCOZENZFDNZGDNZPZUTLQZMQZCOZUSRZVDDNZVEDNZPZSZMATLATZUQURUAZVCUTUSAN
        USDNUGVLLMABCDEUSIJHKUBUCVMVLUSUSRZVCUSUDVKVNVCSFVECOZUSRZVAVIPZSLMFGAA
        VDFRZVGVPVJVQVRVFVOUSVDFVECUEUHVRVHVAVIVDFDUFUIUJVEGRZVPVNVQVCVSVOUSUSV
        EGFCUKUHVSVIVBVAVEGDUFULUJUMUNUOUP $.
        $( [4-Dec-2014] $)
    $}

    ${
      irredneg.n $e |- N = ( invg ` R ) $.
      $( The negative of an irreducible element is irreducible.  (Contributed
         by Mario Carneiro, 4-Dec-2014.) $)
      irredneg $p |- ( ( R e. Ring /\ X e. I ) -> ( N ` X ) e. I ) $=
        ( crg wcel wa cur cfv cmulr cbs eqid simpl irredcl adantl rngnegr cui
        co 1unit unitnegcl mpdan adantr irredrmul mpd3an3 eqeltrrd ) AGHZDBHZIZ
        DAJKZCKZALKZTZDCKBUJAMKZAUMUKCDUONZUMNZUKNZFUHUIOUIDUOHUHUOABDEUPPQRUHU
        IULASKZHZUNBHUHUTUIUHUKUSHUTAUSUKUSNZURUAAUSCUKVAFUBUCUDAUMUSBDULEVAUQU
        EUFUG $.
        $( [4-Dec-2014] $)

      irrednegb.b $e |- B = ( Base ` R ) $.
      $( An element is irreducible iff its negative is.  (Contributed by Mario
         Carneiro, 4-Dec-2014.) $)
      irrednegb $p |- ( ( R e. Ring /\ X e. B ) ->
        ( X e. I <-> ( N ` X ) e. I ) ) $=
        ( crg wcel cfv irredneg adantlr wceq cgrp rnggrp grpinvinv sylan adantr
        wa eqeltrrd impbida ) BIJZEAJZTZECJZEDKZCJZUCUFUHUDBCDEFGLMUEUHTUGDKZEC
        UEUIENZUHUCBOJUDUJBPABDEHGQRSUCUHUICJUDBCDUGFGLMUAUB $.
        $( [4-Dec-2014] $)
    $}
  $}

  ${
    $d x y A $.  $d x y I $.  $d x y Z $.
    prmirred.1 $e |- Z = ( CCfld |`s ZZ ) $.
    prmirred.2 $e |- I = ( Irred ` Z ) $.
    $( A natural number is irreducible over ` ZZ ` iff it is a prime number. $)
    prmirredlem $p |- ( A e. NN -> ( A e. I <-> A e. Prime ) ) $=
      ( vy wcel cprime wa cfv wbr c1 wceq cz co cmul cc0 wb ad2antrr syl vx cuz
      cn c2 cv cdivides wo wral wne crg ccnfld csubrg zsubrg subrgrng ax-mp cur
      wi cnfld1 subrg1 irredn1 mpan anim2i eluz2b3 sylibr cui cdiv nnz ad2antrl
      simprr nnne0 divides2 syl3anc cc zcn nncn divcan2 simplr eqeltrd subrgbas
      mpbid cbs eqid cnfldmul ressmulr irredmul cabs zrngunit baib cn0 nnnn0 cr
      cle nn0re nn0ge0 absid syl2anc eqeq1d bitrd nnre simprl nndivre clt nngt0
      3syl divge0 syl22anc ax-1cn a1i divmul syl112anc mulid1 orbi12d ralrimiva
      3bitrd expr isprm2 sylanbrc wn prmz wnel 1nprm df-nel mpbi prmnn id eleq1
      syl5ibcom adantld syl5bi mtoi simpr simplrr mul02 oveq1 sylib eqeq1 abscl
      3netr4d mpbird recnd necon3i simplrl abs00 necon3bid df-ne nn0abscl elnn0
      ord simprbi dvdsmul1 ad2antlr breqtrd absdvdsb breq1 imbi12d rcla4v syl3c
      mulcan absmul fveq2d eqtrd eqtr3d eqeq12d eqcom syl6bb 3bitr2d ralrimivva
      mt3d ex isirred2 syl3anbrc adantl impbida ) AUCGZABGZAHGZUVNUVOIZAUDUBJGZ
      FUEZAUFKZUVSLMZUVSAMZUGZUQZFUCUHZUVPUVQUVNALUIZIUVRUVOUWFUVNCUJGZUVOUWFNU
      KULJGZUWGUMNUKCDUNUOCBLAEUWHLCUPJMUMNUKCLDURUSUOUTVAVBAVCVDUVQUWDFUCUVQUV
      SUCGZUVTUWCUVQUWIUVTIZIZUVSCVEJZGZAUVSVFOZUWLGZUGZUWCUWKUVSNGZUWNNGZUVSUW
      NPOZBGUWPUWIUWQUVQUVTUVSVGVHZUWKUVTUWRUVQUWIUVTVIUWKUWQUVSQUIZANGZUVTUWRR
      UWTUWIUXAUVQUVTUVSVJVHZUVNUXBUVOUWJAVGSZUVSAVKVLVTZUWKUWSABUWKAVMGZUVSVMG
      ZUXAUWSAMUWKUXBUXFUXDAVNTZUWIUXGUVQUVTUVSVOVHZUXCAUVSVPVLUVNUVOUWJVQVRNCP
      UWLBUVSUWNEUWHNCWAJMUMNUKCDVSUOZUWLWBZNUKCPDWCWDZWEVLUWKUWMUWAUWOUWBUWKUW
      MUVSWFJZLMZUWAUWKUWQUWMUXNRZUWTUWMUWQUXNUVSCDWGWHZTUWKUXMUVSLUWIUXMUVSMZU
      VQUVTUWIUVSWIGZUXQUVSWJZUXRUVSWKGZQUVSWLKUXQUVSWMZUVSWNUVSWOWPTVHWQWRUWKU
      WOUWNWFJZLMZUWBUWKUWRUWOUYCRUXEUWOUWRUYCUWNCDWGWHTUWKUYCUWNLMZUVSLPOZAMZU
      WBUWKUYBUWNLUWKUWNWKGZQUWNWLKZUYBUWNMUWKAWKGZUWIUYGUVNUYIUVOUWJAWSSZUVQUW
      IUVTWTZAUVSXAWPUWKUYIQAWLKZUXTQUVSXBKZUYHUYJUVNUYLUVOUWJUVNAWIGZUYLAWJZAW
      NZTSUWKUWIUXRUXTUYKUXSUYAXDUWIUYMUVQUVTUVSXCVHAUVSXEXFUWNWOWPWQUWKUXFLVMG
      ZUXGUXAUYDUYFRUXHUYQUWKXGXHUXIUXCALUVSXIXJUWKUYEUVSAUWKUXGUYEUVSMUXIUVSXK
      TWQXNWRXLVTXOXMFAXPZXQUVPUVOUVNUVPUXBAUWLGZXRUAUEZUVSPOZAMZUYTUWLGZUWMUGZ
      UQZFNUHUANUHUVOAXSZUVPUYSLHGZLHXTVUGXRYALHYBYCUYSUXBAWFJZLMZIUVPVUGACDWGU
      VPVUIVUGUXBUVPVUHHGVUIVUGUVPVUHAHUVPUVNUYNVUHAMZAYDZUYOUYNUYIUYLVUJAWMUYP
      AWOWPXDZUVPYEVRVUHLHYFYGYHYIYJUVPVUEUAFNNUVPUYTNGZUWQIZIZVUBVUDVUOVUBIZVU
      DUYTWFJZLMZVUQAMZUGZVUPVUQUCGZUWEVUQAUFKZVUTVUPVVAVUQQMZVUPVUQQUIZVVCXRVU
      PVVDUYTQUIZVUPVUAQUVSPOZUIVVEVUPAQVUAVVFVUPUVNAQUIUVPUVNVUNVUBVUKSAVJTVUO
      VUBYKZVUPUXGVVFQMVUPUWQUXGUVPVUMUWQVUBYLZUVSVNTZUVSYMTYRUYTQVUAVVFUYTQUVS
      PYNUUATVUPVUQQUYTQVUPUYTVMGZVVCUYTQMRVUPVUMVVJUVPVUMUWQVUBUUBZUYTVNTZUYTU
      UCTUUDYSZVUQQUUEYOVUPVVAVVCVUPVUQWIGZVVAVVCUGVUPVUMVVNVVKUYTUUFTVUQUUGYOU
      UHUVHUVPUWEVUNVUBUVPUVRUWEUYRUUISVUPUYTAUFKZVVBVUPUYTVUAAUFVUNUYTVUAUFKUV
      PVUBUYTUVSUUJUUKVVGUULVUPVUMUXBVVOVVBRVVKUVPUXBVUNVUBVUFSUYTAUUMWPVTUWDVV
      BVUTUQFVUQUCUVSVUQMZUVTVVBUWCVUTUVSVUQAUFUUNVVPUWAVURUWBVUSUVSVUQLYPUVSVU
      QAYPXLUUOUUPUUQVUPVUCVURUWMVUSVUPVUMVUCVURRVVKVUCVUMVURUYTCDWGWHTVUPUWMUX
      NVUQUXMPOZVUQLPOZMZVUSVUPUWQUXOVVHUXPTVUPUXMVMGUYQVUQVMGZVVDVVSUXNRVUPUXM
      VUPUXGUXMWKGVVIUVSYQTYTUYQVUPXGXHVUPVUQVUPVVJVUQWKGVVLUYTYQTYTZVVMUXMLVUQ
      UURXJVUPVVSAVUQMVUSVUPVVQAVVRVUQVUPVUAWFJZVVQAVUPVVJUXGVWBVVQMVVLVVIUYTUV
      SUUSWPVUPVWBVUHAVUPVUAAWFVVGUUTUVPVUJVUNVUBVULSUVAUVBVUPVVTVVRVUQMVWAVUQX
      KTUVCAVUQUVDUVEUVFXLYSUVIUVGUAFNCPUWLBAUXJUXKEUXLUVJUVKUVLUVM $.
      $( [5-Dec-2014] $)

    $( The positive irreducible elements of ` ZZ ` are the prime numbers.  This
       is an alternative way to define ` Prime ` .  (Contributed by Mario
       Carneiro, 5-Dec-2014.) $)
    dfprime2 $p |- Prime = ( NN i^i I ) $=
      ( vx cprime cn cin cv wcel wa prmirredlem pm5.32i prmnn pm4.71ri 3bitr4ri
      elin eqriv ) EFGAHZEIZGJZTAJZKUATFJZKTSJUCUAUBUCTABCDLMTGAQUCUATNOPR $.
      $( [5-Dec-2014] $)

    $( The irreducible elements of ` ZZ ` are exactly the prime numbers (and
       their negatives).  (Contributed by Mario Carneiro, 5-Dec-2014.) $)
    prmirred $p |- ( A e. I <-> ( A e. ZZ /\ ( abs ` A ) e. Prime ) ) $=
      ( wcel cz wa cfv cprime ccnfld wceq zsubrg ax-mp wb cc0 wi mpan cle wbr
      cabs csubrg cbs subrgbas irredcl pm4.71ri cn0 cr cn elznn0nn biimpi elnn0
      cneg wo ax-1 crg wne subrgrng csubg c0g subrgsubg cnfld0 irredn0 necon2bi
      subg0 pm2.21d jaoi sylbi prmnn prmirredlem pm5.21ndd nn0re nn0ge0 syl2anc
      a1i absid eleq1d bitr4d adantl cminusg eqid irrednegb subginv cc cnfldneg
      zcn syl eqtr3d bitrd adantr zre nnnn0 le0neg1 mpbird absnid 3bitr4d mpdan
      adantrl jaodan pm5.32i bitri ) ABFZAGFZXBHXCAUAIZJFZHXBXCGCBAEGKUBIFZGCUC
      ILMGKCDUDNZUEUFXCXBXEXCAUGFZAUHFZAUMZUIFZHZUNZXBXEOZXCXMAUJUKXCXHXNXLXHXN
      XCXHXBAJFZXEXHAUIFZXBXOXHXPAPLZUNXBXPQZAULXPXRXQXPXBUOXQXBXPXBAPCUPFZXBAP
      UQXFXSMGKCDURNZCBAPEGKUSIFZPCUTILXFYAMGKVANZGKCPDVBVENVCRVDVFVGVHXOXPQXHA
      VIVOXPXBXOOQXHABCDEVJVOVKXHXDAJXHXIPASTXDALAVLAVMAVPVNVQVRVSXCXKXNXIXCXKH
      ZXJBFZXJJFZXBXEXKYDYEOXCXJBCDEVJVSXCXBYDOXKXCXBACVTIZIZBFZYDXSXCXBYHOXTGC
      BYFAEYFWAZXGWBRXCYGXJBXCAKVTIZIZYGXJYAXCYKYGLYBGKCYJYFADYJWAYIWCRXCAWDFYK
      XJLAWFAWEWGWHVQWIWJYCXDXJJYCXIAPSTZXDXJLXCXIXKAWKWJZYCYLPXJSTZXKYNXCXKXJU
      GFYNXJWLXJVMWGVSYCXIYLYNOYMAWMWGWNAWOVNVQWPWRWSWQWTXA $.
      $( [5-Dec-2014] $)
  $}

  ${
    $d r x y A $.  $d f r w B $.  $d f r w F $.  $d f r w x y V $.  $d x y X $.
    $d f r s t w x y S $.  $d f r s t w x y T $.  $d f r s t w x y W $.
    $d y Y $.
    isassa.v $e |- V = ( Base ` W ) $.
    isassa.f $e |- F = ( Scalar ` W ) $.
    isassa.b $e |- B = ( Base ` F ) $.
    isassa.s $e |- S = ( vsca ` W ) $.
    isassa.t $e |- T = ( .r ` W ) $.
    $( The properties of an associative algebra. $)
    isassa $p |- ( W e. AssAlg <-> ( ( W e. LMod /\ W e. Ring /\ F e. CRing )
                   /\ A. r e. B A. x e. V A. y e. V
      ( ( ( r S x ) T y ) = ( r S ( x T y ) ) /\
        ( x T ( r S y ) ) = ( r S ( x T y ) ) ) ) ) $=
      ( wcel cv co wceq wa cfv vf vs vt casa clmod crg cin ccrg wral cmulr wsbc
      vw w3a cvsca cbs csca cvv fvex fveq2 syl6eqr simpr eleq1d fveq2d ad2antrr
      a1i wb eqtrd simplr oveqd eqidd oveq123d eqeq12d anbi12d sbcied raleqbidv
      adantr sbcied2 df-assa elrab2 anass elin anbi1i df-3an bitr4i 3bitr2i ) H
      UDOHUEUFUGZOZFUHOZIPZAPZDQZBPZEQZWIWJWLEQZDQZRZWJWIWLDQZEQZWORZSZBGUIZAGU
      IZICUIZSZSWGWHSZXCSHUEOZHUFOZWHUMZXCSUAPZUHOZWIWJUBPZQZWLUCPZQZWIWJWLXMQZ
      XKQZRZWJWIWLXKQZXMQZXPRZSZUCULPZUJTZUKZUBYBUNTZUKZBYBUOTZUIZAYGUIZIXIUOTZ
      UIZSZUAYBUPTZUKXDULHWFUDYBHRZYLXDUAYMFUQYMUQOYNYBUPURVEYNYMHUPTFYBHUPUSKU
      TYNXIFRZSZXJWHYKXCYPXIFUHYNYOVAZVBYPYIXBIYJCYPYJFUOTCYPXIFUOYQVCLUTYNYIXB
      VFYOYNYHXAAYGGYNYGHUOTGYBHUOUSJUTZYNYFWTBYGGYRYNYDWTUBYEUQYEUQOYNYBUNURVE
      YNXKYERZSZYAWTUCYCUQYCUQOYTYBUJURVEYTXMYCRZSZXQWPXTWSUUBXNWMXPWOUUBXLWKWL
      WLXMEUUBXMYCEYTUUAVAUUBYCHUJTZEYNYCUUCRYSUUAYBHUJUSVDNUTVGZUUBXKDWIWJUUBX
      KYEDYNYSUUAVHUUBYEHUNTZDYNYEUUERYSUUAYBHUNUSVDMUTVGZVIUUBWLVJVKUUBWIWIXOW
      NXKDUUFUUBWIVJUUBXMEWJWLUUDVIVKZVLUUBXSWRXPWOUUBWJWJXRWQXMEUUDUUBWJVJUUBX
      KDWIWLUUFVIVKUUGVLVMVNVNVOVOVPVOVMVQABULUCUAUBIVRVSWGWHXCVTXEXHXCXEXFXGSZ
      WHSXHWGUUHWHHUEUFWAWBXFXGWHWCWDWBWE $.
      $( [29-Dec-2014] $)

    $( The properties of an associative algebra. $)
    assalem $p |- ( ( W e. AssAlg /\ ( A e. B /\ X e. V /\ Y e. V ) ) ->
      ( ( ( A S X ) T Y ) = ( A S ( X T Y ) ) /\
        ( X T ( A S Y ) ) = ( A S ( X T Y ) ) ) ) $=
      ( vr wcel co wceq oveq1 eqeq12d vx vy casa cv wa wral w3a crg ccrg isassa
      clmod simprbi oveq1d oveq2d anbi12d oveq2 rcla43v impcom sylan ) GUCPZOUD
      ZUAUDZCQZUBUDZDQZVAVBVDDQZCQZRZVBVAVDCQZDQZVGRZUEZUBFUFUAFUFOBUFZABPHFPIF
      PUGZAHCQZIDQZAHIDQZCQZRZHAICQZDQZVRRZUEZUTGUKPGUHPEUIPUGVMUAUBBCDEFGOJKLM
      NUJULVNVMWCVLWCAVBCQZVDDQZAVFCQZRZVBAVDCQZDQZWFRZUEVOVDDQZAHVDDQZCQZRZHWH
      DQZWMRZUEOUAUBAHIBFFVAARZVHWGVKWJWQVEWEVGWFWQVCWDVDDVAAVBCSUMVAAVFCSZTWQV
      JWIVGWFWQVIWHVBDVAAVDCSUNWRTUOVBHRZWGWNWJWPWSWEWKWFWMWSWDVOVDDVBHACUPUMWS
      VFWLACVBHVDDSUNZTWSWIWOWFWMVBHWHDSWTTUOVDIRZWNVSWPWBXAWKVPWMVRVDIVODUPXAW
      LVQACVDIHDUPUNZTXAWOWAWMVRXAWHVTHDVDIACUPUNXBTUOUQURUS $.
      $( [29-Dec-2014] $)

    $( Left-associative property of an associative algebra. $)
    assaass $p |- ( ( W e. AssAlg /\ ( A e. B /\ X e. V /\ Y e. V ) ) ->
      ( ( A S X ) T Y ) = ( A S ( X T Y ) ) ) $=
      ( casa wcel w3a wa co wceq assalem simpld ) GOPABPHFPIFPQRAHCSIDSAHIDSCSZ
      THAICSDSUCTABCDEFGHIJKLMNUAUB $.
      $( [29-Dec-2014] $)

    $( Right-associative property of an associative algebra. $)
    assaassr $p |- ( ( W e. AssAlg /\ ( A e. B /\ X e. V /\ Y e. V ) ) ->
      ( X T ( A S Y ) ) = ( A S ( X T Y ) ) ) $=
      ( casa wcel w3a wa co wceq assalem simprd ) GOPABPHFPIFPQRAHCSIDSAHIDSCSZ
      THAICSDSUCTABCDEFGHIJKLMNUAUB $.
      $( [29-Dec-2014] $)
  $}

  ${
    $d x y z W $.  $d z F $.
    $( An associative algebra is a left module. $)
    assalmod $p |- ( W e. AssAlg -> W e. LMod ) $=
      ( vz vx vy casa wcel clmod crg csca cfv ccrg w3a cv cvsca cmulr wceq wral
      co cbs eqid wa isassa simplbi simp1d ) AEFZAGFZAHFZAIJZKFZUEUFUGUILBMZCMZ
      ANJZRDMZAOJZRUJUKUMUNRULRZPUKUJUMULRUNRUOPUADASJZQCUPQBUHSJZQCDUQULUNUHUP
      ABUPTUHTUQTULTUNTUBUCUD $.
      $( [5-Dec-2014] $)

    $( An associative algebra is a ring. $)
    assarng $p |- ( W e. AssAlg -> W e. Ring ) $=
      ( vz vx vy casa wcel clmod crg csca cfv ccrg w3a cv cvsca cmulr wceq wral
      co cbs eqid wa isassa simplbi simp2d ) AEFZAGFZAHFZAIJZKFZUEUFUGUILBMZCMZ
      ANJZRDMZAOJZRUJUKUMUNRULRZPUKUJUMULRUNRUOPUADASJZQCUPQBUHSJZQCDUQULUNUHUP
      ABUPTUHTUQTULTUNTUBUCUD $.
      $( [5-Dec-2014] $)

    assasca.f $e |- F = ( Scalar ` W ) $.
    $( An associative algebra's scalar field is a commutative ring. $)
    assasca $p |- ( W e. AssAlg -> F e. CRing ) $=
      ( vz vx vy casa wcel clmod crg ccrg w3a cv cvsca cfv wceq cbs wral eqid
      co cmulr wa isassa simplbi simp3d ) BGHZBIHZBJHZAKHZUFUGUHUILDMZEMZBNOZTF
      MZBUAOZTUJUKUMUNTULTZPUKUJUMULTUNTUOPUBFBQOZREUPRDAQOZREFUQULUNAUPBDUPSCU
      QSULSUNSUCUDUE $.
      $( [7-Jan-2015] $)
  $}

  ${
    $d r x y B $.  $d r x y ph $.  $d x y V $.  $d r x y W $.
    isassad.v $e |- ( ph -> V = ( Base ` W ) ) $.
    isassad.f $e |- ( ph -> F = ( Scalar ` W ) ) $.
    isassad.b $e |- ( ph -> B = ( Base ` F ) ) $.
    isassad.s $e |- ( ph -> S = ( vsca ` W ) ) $.
    isassad.t $e |- ( ph -> T = ( .r ` W ) ) $.
    isassad.1 $e |- ( ph -> W e. LMod ) $.
    isassad.2 $e |- ( ph -> W e. Ring ) $.
    isassad.3 $e |- ( ph -> F e. CRing ) $.
    isassad.4 $e |- ( ( ph /\ ( r e. B /\ x e. V /\ y e. V ) ) ->
      ( ( r S x ) T y ) = ( r S ( x T y ) ) ) $.
    isassad.5 $e |- ( ( ph /\ ( r e. B /\ x e. V /\ y e. V ) ) ->
      ( x T ( r S y ) ) = ( r S ( x T y ) ) ) $.
    $( Sufficient condition for being an associative algebra. $)
    isassad $p |- ( ph -> W e. AssAlg ) $=
      ( clmod wcel crg csca cfv ccrg w3a cv cvsca co cmulr wceq wa cbs eqeltrrd
      wral casa 3jca jca ralrimivvva eqtrd oveqd eqidd oveq123d eqeq12d anbi12d
      fveq2d raleqbidv mpbid eqid isassa sylanbrc ) AIUAUBZIUCUBZIUDUEZUFUBZUGJ
      UHZBUHZIUIUEZUJZCUHZIUKUEZUJZVQVRWAWBUJZVSUJZULZVRVQWAVSUJZWBUJZWEULZUMZC
      IUNUEZUPZBWKUPZJVOUNUEZUPZIUQUBAVMVNVPPQAGVOUFLRUOURAVQVREUJZWAFUJZVQVRWA
      FUJZEUJZULZVRVQWAEUJZFUJZWSULZUMZCHUPZBHUPZJDUPWOAXDJBCDHHAVQDUBVRHUBWAHU
      BUGUMWTXCSTUSUTAXFWMJDWNADGUNUEWNMAGVOUNLVGVAAXEWLBHWKKAXDWJCHWKKAWTWFXCW
      IAWQWCWSWEAWPVTWAWAFWBOAEVSVQVRNVBAWAVCVDAVQVQWRWDEVSNAVQVCAFWBVRWAOVBVDZ
      VEAXBWHWSWEAVRVRXAWGFWBOAVRVCAEVSVQWANVBVDXGVEVFVHVHVHVIBCWNVSWBVOWKIJWKV
      JVOVJWNVJVSVJWBVJVKVL $.
      $( [5-Dec-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z L $.  $d x y z S $.  $d x y z W $.
    issubassa.s $e |- S = ( W |`s A ) $.
    issubassa.l $e |- L = ( LSubSp ` W ) $.
    issubassa.v $e |- V = ( Base ` W ) $.
    issubassa.o $e |- O = ( 1r ` W ) $.
    $( The subalgebras of an associative algebra are exactly the subrings
       (under the ring multiplication) that are simultaneously subspaces (under
       the scalar multiplication from the vector space). $)
    issubassa $p |- ( ( W e. AssAlg /\ O e. A /\ A C_ V ) ->
      ( S e. AssAlg <-> ( A e. ( SubRing ` W ) /\ A e. L ) ) ) $=
      ( wcel cfv wa crg co jca wceq eqid a1i cv vy vz casa wss w3a csubrg cress
      simpl1 assarng syl adantl syl5eqelr simpl3 simpl2 sylanbrc clmod assalmod
      vx issubrg wb islss3 3syl mpbir2and csca cbs cvsca cmulr subrgss ad2antrl
      ressbas2 resssca eqidd ressvsca ressmulr simpr lsslmod syl2an ccrg adantr
      subrgrng assasca simpll simpr1 simpr2 assaass syl13anc assaassr 3ad2antl1
      sseldd simpr3 isassad impbida ) FUCKZDAKZAEUDZUEZBUCKZAFUFLKZACKZMZWPWQMZ
      WRWSXAFNKZFAUGOZNKZMWOWNMWRXAXBXDXAWMXBWMWNWOWQUHZFUIUJXAXCBNGWQBNKZWPBUI
      UKULPXAWOWNWMWNWOWQUMZWMWNWOWQUNPAEFDIJUSUOXAWSWOBUPKZXGWQXHWPBUQUKXAWMFU
      PKZWSWOXHMUTXEFUQZCAEFBGIHVAVBVCPWMWNWTWQWOWMWTMZUAUBFVDLZVELZFVFLZFVGLZX
      LABURXKWOABVELQWRWOWMWSAEFIVHVIZAEBFGIVJUJXLBVDLQXKAXLFBGXLRZVKSXKXMVLXNB
      VFLQXKAXNFBGXNRZVMSXOBVGLQXKAFBXOGXORZVNSWMXIWSXHWTXJWRWSVOCAFBGHVPVQWRXF
      WMWSAFBGVTVIWMXLVRKWTXLFXQWAVSXKURTZXMKZUATZAKZUBTZAKZUEZMZWMYAYBEKZYDEKZ
      XTYBXNOYDXOOXTYBYDXOOXNOZQWMWTYFWBZXKYAYCYEWCZYGAEYBXKWOYFXPVSZXKYAYCYEWD
      WIZYGAEYDYMXKYAYCYEWJWIZXTXMXNXOXLEFYBYDIXQXMRZXRXSWEWFYGWMYAYHYIYBXTYDXN
      OXOOYJQYKYLYNYOXTXMXNXOXLEFYBYDIXQYPXRXSWGWFWKWHWL $.
      $( [7-Jan-2015] $)
  $}

  ${
    $d s t w L $.  $d s t S $.  $d t T $.  $d s t w V $.  $d s t w W $.
    aspval.a $e |- A = ( AlgSpan ` W ) $.
    aspval.v $e |- V = ( Base ` W ) $.
    ${
      aspval.l $e |- L = ( LSubSp ` W ) $.
      $( Value of the algebraic closure operation inside an associative
         algebra. $)
      aspval $p |- ( ( W e. AssAlg /\ S C_ V ) -> ( A ` S ) =
        |^| { t e. ( ( SubRing ` W ) i^i L ) | S C_ t } ) $=
        ( vs vw wcel wss cfv cv csubrg crab cint wceq cbs casa wa cpw cmpt casp
        cin clss fveq2 syl6eqr pweqd ineq12d rabeq inteqd mpteq12dv df-asp fvex
        syl cvv eqeltri pwex mptex fvmpt syl5eq fveq1d adantr simpr sylibr wrex
        elpw2 assarng subrgid clmod assalmod lss1 elin sylanbrc rcla4ev syl2anc
        crg sseq2 intexrab sylib sseq1 rabbidv eqid fvmptg eqtrd ) FUALZCEMZUBZ
        CBNZCJEUCZJOZAOZMZAFPNZDUFZQZRZUDZNZCWNMZAWQQZRZWHWKXASWIWHCBWTWHBFUENW
        TGKFJKOZTNZUCZWOAXEPNZXEUGNZUFZQZRZUDWTUAUEXEFSZJXGXLWLWSXMXFEXMXFFTNZE
        XEFTUHHUIUJXMXKWRXMXJWQSXKWRSXMXHWPXIDXEFPUHXMXIFUGNDXEFUGUHIUIUKWOAXJW
        QULUQUMUNKAJUOJWLWSEEXNURHFTUPUSZUTVAVBVCVDVEWJCWLLZXDURLZXAXDSWJWIXPWH
        WIVFZCEXOVIVGWJXBAWQVHZXQWJEWQLZWIXSWHXTWIWHEWPLZEDLZXTWHFVSLYAFVJEFHVK
        UQWHFVLLYBFVMDEFHIVNUQEWPDVOVPVEXRXBWIAEWQWNECVTVQVRXBAWQWAWBJCWSXDWLUR
        WTWMCSZWRXCYCWOXBAWQWMCWNWCWDUMWTWEWFVRWG $.
        $( [7-Jan-2015] $)

      $( The algebraic span of a set of vectors is a vector subspace. $)
      asplss $p |- ( ( W e. AssAlg /\ S C_ V ) -> ( A ` S ) e. L ) $=
        ( vt casa wcel wss wa cfv cv csubrg cin crab cint cvv clmod c0 assalmod
        aspval wne adantr ssrab2 inss2 sstri a1i fvex syl6eqelr sylibr lssintcl
        intex syl3anc eqeltrd ) EJKZBDLZMZBANZBIOLZIEPNZCQZRZSZCIABCDEFGHUDZUTE
        UAKZVECLZVEUBUEZVFCKURVHUSEUCUFVIUTVEVDCVBIVDUGVCCUHUIUJUTVFTKVJUTVFVAT
        VGBAUKULVEUOUMVECEHUNUPUQ $.
        $( [7-Jan-2015] $)

      $( The algebraic span of a subalgebra is itself.  ( ~ spanid analog.) $)
      aspid $p |- ( ( W e. AssAlg /\ S e. ( SubRing ` W ) /\ S e. L ) ->
        ( A ` S ) = S ) $=
        ( vt casa wcel csubrg cfv w3a cv wss cin crab cint wceq 3ad2ant2 aspval
        simp1 subrgss syl2anc wa 3simpc elin sylibr intmin syl eqtrd ) EJKZBELM
        ZKZBCKZNZBAMZBIOPIUNCQZRSZBUQUMBDPZURUTTUMUOUPUCUOUMVAUPBDEGUDUAIABCDEF
        GHUBUEUQBUSKZUTBTUQUOUPUFVBUMUOUPUGBUNCUHUIIBUSUJUKUL $.
        $( [7-Jan-2015] $)
    $}

    $( The algebraic span of a set of vectors is a subring of the algebra. $)
    aspsubrg $p |- ( ( W e. AssAlg /\ S C_ V ) ->
      ( A ` S ) e. ( SubRing ` W ) ) $=
      ( vt casa wcel wss wa cfv cv csubrg clss cin crab cint eqid cvv aspval c0
      wne ssrab2 inss1 sstri a1i fvex syl6eqelr sylibr subrgint syl2anc eqeltrd
      intex ) DHIBCJKZBALZBGMJZGDNLZDOLZPZQZRZURGABUSCDEFUSSUAZUOVAURJZVAUBUCZV
      BURIVDUOVAUTURUQGUTUDURUSUEUFUGUOVBTIVEUOVBUPTVCBAUHUIVAUNUJDVAUKULUM $.
      $( [7-Jan-2015] $)

    $( Span preserves subset ordering.  ( ~ spanss analog.) $)
    aspss $p |- ( ( W e. AssAlg /\ S C_ V /\ T C_ S ) ->
      ( A ` T ) C_ ( A ` S ) ) $=
      ( vt casa wcel wss w3a cv csubrg cfv crab cint syl wceq aspval clss wa wi
      cin simpl3 sstr2 ss2rabdv intss simp1 simp3 simp2 syl2anc 3adant3 3sstr4d
      sstrd eqid ) EIJZBDKZCBKZLZCHMZKZHENOEUAOZUDZPZQZBVAKZHVDPZQZCAOZBAOZUTVH
      VEKVFVIKUTVGVBHVDUTVAVDJZUBUSVGVBUCUQURUSVLUECBVAUFRUGVHVEUHRUTUQCDKVJVFS
      UQURUSUIUTCBDUQURUSUJUQURUSUKUOHACVCDEFGVCUPZTULUQURVKVISUSHABVCDEFGVMTUM
      UN $.
      $( [7-Jan-2015] $)

    $( A set of vectors is a subset of its span.  ( ~ spanss2 analog.) $)
    aspssid $p |- ( ( W e. AssAlg /\ S C_ V ) -> S C_ ( A ` S ) ) $=
      ( vt casa wcel wss wa cv csubrg cfv clss cin crab cint ssintub eqid
      aspval syl5sseqr ) DHIBCJKBGLJGDMNDONZPZQRBBANGBUDSGABUCCDEFUCTUAUB $.
      $( [7-Jan-2015] $)
  $}

