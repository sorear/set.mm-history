$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                  Partitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    prtlem60.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    prtlem60.2 $e |- ( ps -> ( th -> ta ) ) $.
    $( Lemma for ~ prter3 . $)
    prtlem60 $p |- ( ph -> ( ps -> ( ch -> ta ) ) ) $=
      ( wi a1i syldd ) ABCDEFBDEHHAGIJ $.
      $( [9-Oct-2010] $)
  $}

  ${
    impbiddOLD.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    impbiddOLD.2 $e |- ( ph -> ( ps -> ( th -> ch ) ) ) $.
    $( Lemma for ~ prter3 .  (Moved to ~ impbidd in main set.mm and may be
       deleted by mathbox owner, RM. --NM 15-May-2013.) $)
    impbiddOLD $p |- ( ph -> ( ps -> ( ch <-> th ) ) ) $=
      ( impbidd ) ABCDEFG $.
      $( [12-Oct-2010] $)
  $}

  ${
    bicomdd.1 $e |- ( ph -> ( ps -> ( ch <-> th ) ) ) $.
    $( Commute two sides of a biconditional in a deduction.  (Contributed by
       Rodolfo Medina, 19-Oct-2010.)  (The proof was shortened by Andrew
       Salmon, 29-Jun-2011.) $)
    bicomdd $p |- ( ph -> ( ps -> ( th <-> ch ) ) ) $=
      ( wb bicom syl6ib ) ABCDFDCFECDGH $.
      $( [29-Jun-2011] $) $( [19-Oct-2010] $)

    $( Commute two sides of a biconditional in a deduction.  (Contributed by
       Rodolfo Medina, 19-Oct-2010.) $)
    bicomddOLD $p |- ( ph -> ( ps -> ( th <-> ch ) ) ) $=
      ( wb wa imp bicomd ex ) ABDCFABGCDABCDFEHIJ $.
      $( [19-Oct-2010] $)
  $}

  ${
    prtlem1.1 $e |- ( ps -> ( ch -> ph ) ) $.
    $( Add a disjunct in the antecedent.  (Contributed by Rodolfo Medina,
       24-Sep-2010.) $)
    prtlem1 $p |- ( ( ph \/ ps ) -> ( ch -> ph ) ) $=
      ( wi ax-1 jaoi ) ACAEBACFDG $.
      $( [24-Sep-2010] $)
  $}

  ${
    jca2.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca2.2 $e |- ( ps -> th ) $.
    $( Inference conjoining the consequents of two implications.  (Contributed
       by Rodolfo Medina, 12-Oct-2010.) $)
    jca2 $p |- ( ph -> ( ps -> ( ch /\ th ) ) ) $=
      ( wi a1i jcad ) ABCDEBDGAFHI $.
      $( [12-Oct-2010] $)
  $}

  ${
    jca2r.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca2r.2 $e |- ( ps -> th ) $.
    $( Inference conjoining the consequents of two implications.  (Contributed
       by Rodolfo Medina, 17-Oct-2010.) $)
    jca2r $p |- ( ph -> ( ps -> ( th /\ ch ) ) ) $=
      ( wi a1i jcad ) ABDCBDGAFHEI $.
      $( [17-Oct-2010] $)
  $}

  ${
    jca3.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca3.2 $e |- ( th -> ta ) $.
    $( Inference conjoining the consequents of two implications.  (Contributed
       by Rodolfo Medina, 14-Oct-2010.) $)
    jca3 $p |- ( ph -> ( ps -> ( th -> ( ch /\ ta ) ) ) ) $=
      ( wa wi imp a1d jca2 ex ) ABDCEHIABHZDCENCDABCFJKGLM $.
      $( [14-Oct-2010] $)
  $}

  ${
    prtlem50.1 $e |- ( ph -> ( ps -> ch ) ) $.
    prtlem50.2 $e |- ( th -> ta ) $.
    $( Lemma for ~ prter3 . $)
    prtlem50 $p |- ( ph -> ( ( ps /\ th ) -> ( ch /\ ta ) ) ) $=
      ( wi a1i anim12d ) ABCDEFDEHAGIJ $.
      $( [12-Oct-2010] $)
  $}

  $( Rearrangement of 4 conjuncts.  (Contributed by Rodolfo Medina,
     24-Sep-2010.)  (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
  an43 $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
               ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) $=
    ( wa an42 bicomi ) ADEBCEEABECDEEADBCFG $.
    $( [29-Jun-2011] $) $( [24-Sep-2010] $)

  $( Rearrangement of 4 conjuncts.  (Contributed by Rodolfo Medina,
     24-Sep-2010.) $)
  an43OLD $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
               ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) $=
    ( wa ancom anbi2i an4 bitri ) ABEZCDEZEJDCEZEADEBCEEKLJCDFGABDCHI $.
    $( [24-Sep-2010] $)

  $( A rearrangement of conjuncts.  (Contributed by Rodolfo Medina,
     25-Sep-2010.) $)
  an3 $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) -> ( ph /\ th ) ) $=
    ( wa an43 simplbi ) ABECDEEADEBCEABCDFG $.
    $( [25-Sep-2010] $)

  $( Lemma for ~ prter3 : a rearrangement of conjuncts. $)
  prtlem70 $p |- ( ( ( ( ps /\ et ) /\ ( ( ph /\ th ) /\ ( ch /\ ta ) ) )
                        /\ ph ) <-> ( ( ph /\ ( ps /\ ( ch /\ ( th /\ ta ) ) )
    ) /\ et ) ) $=
    ( anass anbi1i anandi ancom bitr4i 3bitr2i 3bitri 3bitr4ri an4 bitri anbi2i
    wa ) BFRZADRZCERZRZRZARZABDRZRZUARZFRZAUEUARZRZFRABCDERZRRZRZFRABRZTRZUARZF
    RUNUBRZFRZUHUDUPUQFUNTUAGHUGUPFUFUOUAABDIHHUDFUNRZUBRZFUQRURUDASRZUBRZUNFRZ
    UBRUTUDAUCRVBUCAJASUBGKVCVAUBABFGHVCUSUBUNFJHLFUNUBGFUQJMNUGUJFAUEUAGHUJUMF
    UIULAUIBCRUKRULBDCEOBCUKGPQHM $.
    $( [20-Oct-2010] $)

  ${
    ibdr.1 $e |- ( ph -> ( ch -> ( ps <-> ch ) ) ) $.
    $( Reverse of ibd.  (Contributed by Rodolfo Medina, 30-Sep-2010.) $)
    ibdr $p |- ( ph -> ( ch -> ps ) ) $=
      ( bicomdd ibd ) ACBACBCDEF $.
      $( [30-Sep-2010] $)
  $}

  $( Variant of ~ pm5.31 .  (Contributed by Rodolfo Medina, 15-Oct-2010.) $)
  pm5.31r $p |- ( ( ch /\ ( ph -> ps ) ) -> ( ph -> ( ch /\ ps ) ) ) $=
    ( wi wa pm3.2 imim2d imp ) CABDACBEZDCBIACBFGH $.
    $( [15-Oct-2010] $)

  $( Cancel a conjunct from the scope of an existential quantifier.
     (Contributed by Rodolfo Medina, 25-Sep-2010.)  (The proof was shortened by
     Andrew Salmon, 29-Jun-2011.) $)
  exan3 $p |- ( E. x ( ph /\ ps ) -> E. x ps ) $=
    ( wa simpr eximi ) ABDBCABEF $.
    $( [25-Sep-2010] $)

  $( Cancel a conjunct from the scope of an existential quantifier.
     (Contributed by Rodolfo Medina, 25-Sep-2010.) $)
  exan3OLD $p |- ( E. x ( ph /\ ps ) -> E. x ps ) $=
    ( wa wex 19.40 simprd ) ABDCEACEBCEABCFG $.
    $( [25-Sep-2010] $)

  $( Double the quantifiers of theorem r19.29.  (Contributed by Rodolfo Medina,
     25-Sep-2010.) $)
  2r19.29 $p |- ( ( A. x e. A A. y e. B ph /\ E. x e. A E. y e. B ps )
                        -> E. x e. A E. y e. B ( ph /\ ps ) ) $=
    ( wral wrex wa r19.29 reximi syl ) ADFGZCEGBDFHZCEHIMNIZCEHABIDFHZCEHMNCEJO
    PCEABDFJKL $.
    $( [25-Sep-2010] $)

  $( Lemma for ~ prter3 . $)
  prtlem100 $p |- ( E. x e. A ( B e. x /\ ph ) <->
                        E. x e. ( A \ { (/) } ) ( B e. x /\ ph ) ) $=
    ( cv wcel wa csn cdif wne anass eldifsn anbi1i ne0i pm4.71ri bitri 3bitr4ri
    c0 anbi2i rexbii2 ) DBEZFZAGZUCBCCRHIZUACFZUARJZGZUCGUEUFUCGZGUAUDFZUCGUEUC
    GUEUFUCKUIUGUCUACRLMUCUHUEUCUFUBGZAGUHUBUJAUBUFUADNOMUFUBAKPSQT $.
    $( [19-Oct-2010] $)

  ${
    $d u v x r $.  $d u v x s $.  $d u v x A $.
    $( Lemma for ~ prter1 , ~ prter2 , ~ prter3 and ~ prtex . $)
    prtlem5 $p |- ( [ s / v ] [ r / u ] E. x e. A ( u e. x /\ v e. x )
                        <-> E. x e. A ( r e. x /\ s e. x ) ) $=
      ( cv wcel wrex wsbc wex sbbii bitri sban exbii 3bitri ax-17 sbf anbi12i
      wa df-rex sbex elsb3 anbi2i anbi1i bitr4i ) CGAGZHZBGUGHZTZADIZCFGZJZBEGZ
      JZUGDHZULUGHZUNUGHZTZTZAKZUSADIUOUPBUNJZUQUITZBUNJZTZAKZVBUQBUNJZUIBUNJZT
      ZTZAKZVAUOUPCULJZUJCULJZTZBUNJZAKZUPVCTZBUNJZAKVFUOUPUJTZCULJZAKZBUNJZVTB
      UNJZAKVPUOVSAKZCULJZBUNJWBUMWEBEUKWDCFUJADUALLWEWABEVSACFUBLMVTABEUBWCVOA
      VTVNBEUPUJCFNLOPVOVRAVNVQBEVLUPVMVCUPCFUPCQRVMUHCULJZUICULJZTVCUHUICFNWFU
      QWGUIFCAUCUICFUICQRSMSLOVRVEAUPVCBENOPVEVJAVDVIVBUQUIBENUDOVKVBUSTZAKVAVJ
      WHAVIUSVBVGUQVHURUQBEUQBQREBAUCSUDOWHUTAVBUPUSUPBEUPBQRUEOMPUSADUAUF $.
      $( [25-Sep-2010] $)
  $}

  $( Lemma for ~ prter2 . $)
  prtlem90 $p |- ( -. A e. B -> ( C e. B -> C =/= A ) ) $=
    ( wcel wn wne wceq wa wb wo olc ancom orbi2i sylib xor sylibr eleq1 nsyl ex
    df-ne syl6ibr necom syl6ib ) ABDZEZCBDZACFZCAFUEUFACGZEZUGUEUFUIUEUFHZUDUFI
    ZUHUJUDUFEHZUFUEHZJZUKEUJULUJJUNUJULKUJUMULUEUFLMNUDUFOPACBQRSACTUAACUBUC
    $.
    $( [17-Oct-2010] $)

  $( Lemma for ~ prter2 . $)
  prtlem80 $p |- ( A e. B -> -. A e. ( C \ { A } ) ) $=
    ( wcel csn wn wa cdif wi snidg a1d iman sylib eldif sylnibr ) ABDZACDZAAEZD
    ZFGZACRHDPQSITFPSQABJKQSLMACRNO $.
    $( [17-Oct-2010] $)

  ${
    $d x A $.  $d x u $.
    $( Negated membership of the empty set in another class.  (Contributed by
       Rodolfo Medina, 25-Sep-2010.) $)
    n0el $p |- ( -. (/) e. A <-> A. x e. A E. u u e. x ) $=
      ( cv wcel wn wal wral wi c0 df-ral df-ex ralbii wa alnex imnan albii wrex
      wex 3bitr4ri 0el df-rex bitri notbii ) BDADZEZFBGZFZACHUECEZUHIZAGZUFBSZA
      CHJCEZFZUHACKULUHACUFBLMUIUGNZFZAGUOASZFUKUNUOAOUJUPAUIUGPQUMUQUMUGACRUQA
      BCUAUGACUBUCUDTT $.
      $( [25-Sep-2010] $)
  $}

  ${
    $d x A $.
    ceqsex3.1OLD $e |- ( ps -> A. x ps ) $.
    ceqsex3.2OLD $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Version of ~ ceqsex with an antecedent instead of a hypothesis.  (Use
       ~ ceqsexg instead of this one. --NM 13-Aug-11) (Contributed by Rodolfo
       Medina, 19-Oct-2010.) $)
    ceqsex3OLD $p |- ( A e. _V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( cvv wcel cv wceq wa wex biimpa exlimi isset wal biimprcd alrimi exintr
      wi syl syl5bi com12 impbid2 ) DGHZCIDJZAKZCLZBUGBCEUFABFMNBUEUHUEUFCLZBUH
      CDOBUFATZCPUIUHTBUJCEUFABFQRUFACSUAUBUCUD $.
      $( [19-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsex3v.1OLD $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Version of ~ ceqsexv with an antecedent instead of a hypothesis.  (Use
       ~ ceqsexgv instead of this one. --NM 13-Aug-11) (Contributed by Rodolfo
       Medina, 19-Oct-2010.) $)
    ceqsex3vOLD $p |- ( A e. _V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( cvv ceqsexgv ) ABCDFEG $.
      $( [19-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdvOLD.1 $e |- Rel A $.
    eqrelrdvOLD.2 $e |- Rel B $.
    eqrelrdvOLD.3 $e |- ( ph -> ( <. x , y >. e. A <-> <. x , y >. e. B ) ) $.
    $( Deduce equality of relations from equivalence of membership.  (Moved to
       ~ eqrelrdv in main set.mm and may be deleted by mathbox owner, RM. --NM
       20-Feb-2014.) $)
    eqrelrdvOLD $p |- ( ph -> A = B ) $=
      ( cv cop wcel wb wal wceq alrimivv wrel eqrel mp2an sylibr ) ABICIJZDKTEK
      LZCMBMZDENZAUABCHODPEPUCUBLFGBCDEQRS $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdv2OLD.1 $e |- ( ( ( Rel A /\ Rel B ) /\ ph )
       -> ( <. x , y >. e. A <-> <. x , y >. e. B ) ) $.
    $( Another version of ~ eqrelrdv .  (Moved to ~ eqrelrdv2 in main set.mm
       and may be deleted by mathbox owner, RM. --NM 20-Feb-2014.) $)
    eqrelrdv2OLD $p |- ( ( ( Rel A /\ Rel B ) /\ ph ) -> A = B ) $=
      ( wrel wa wceq cv cop wcel wb wal alrimivv eqrel adantr mpbird ) DGEGHZAH
      ZDEIZBJCJKZDLUBELMZCNBNZTUCBCFOSUAUDMABCDEPQR $.
      $( [30-Sep-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqbrrdv.1 $e |- Rel A $.
    eqbrrdv.2 $e |- Rel B $.
    eqbrrdv.3 $e |- ( ph -> ( x A y <-> x B y ) ) $.
    $( Deduction from extensionality principle for relations.  (Contributed by
       Rodolfo Medina, 10-Oct-2010.) $)
    eqbrrdv $p |- ( ph -> A = B ) $=
      ( cv wbr cop wcel df-br 3bitr3g eqrelrdv ) ABCDEFGABIZCIZDJPQEJPQKZDLRELH
      PQDMPQEMNO $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d x y z $.  $d x y w $.
    $( Closed form of ~ brabsb .  (Contributed by Rodolfo Medina,
       13-Oct-2010.) $)
    brabsb2 $p |- ( R = { <. x , y >. | ph }
                        -> ( z R w <-> [ w / y ] [ z / x ] ph ) ) $=
      ( copab wceq cv wbr cop wcel wsbc breq df-br syl6bb opelopabsb ) FABCGZHZ
      DIZEIZFJZTUAKRLZABTMCUAMSUBTUARJUCTUAFRNTUAROPABCDEQP $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqbrrdv2.1 $e |- ( ( ( Rel A /\ Rel B ) /\ ph )
                        -> ( x A y <-> x B y ) ) $.
    $( Other version of ~ eqbrrdv .  (Contributed by Rodolfo Medina,
       30-Sep-2010.) $)
    eqbrrdv2 $p |- ( ( ( Rel A /\ Rel B ) /\ ph ) -> A = B ) $=
      ( wrel wa wceq cv wbr cop wcel df-br 3bitr3g eqrelrdv2 anabss5 ) DGEGHZAD
      EIRAHZBCDESBJZCJZDKTUAEKTUALZDMUBEMFTUADNTUAENOPQ $.
      $( [30-Sep-2010] $)
  $}

  ${
    $d x y z R $.  $d x y A $.  $d x y B $.
    ersym2.1 $e |- A e. _V $.
    ersym2.2 $e |- B e. _V $.
    $( Version of ~ ersym with an antecedent instead of a hypothesis.
       (Contributed by Rodolfo Medina, 11-Oct-2010.) $)
    ersym2 $p |- ( Er R -> ( A R B -> B R A ) ) $=
      ( wer wbr wa cvv simpl wcel a1i simpr ersym ex ) CFZABCGZBACGPQHZABCIIPQJ
      AIKRDLBIKRELPQMNO $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    ertr2.1 $e |- A e. _V $.
    ertr2.2 $e |- B e. _V $.
    ertr2.3 $e |- C e. _V $.
    $( Version of ~ ertr with an antecedent instead of a hypothesis.
       (Contributed by Rodolfo Medina, 10-Oct-2010.) $)
    ertr2 $p |- ( Er R -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( wer cvv id1 wcel a1i ertr ) DHZABCDIIINJAIKNELBIKNFLCIKNGLM $.
      $( [10-Oct-2010] $)
  $}

  ${
    ersymb2.1 $e |- A e. _V $.
    ersymb2.2 $e |- B e. _V $.
    $( Version of ~ ersymb with an antecedent instead of a hypothesis.
       (Contributed by Rodolfo Medina, 11-Oct-2010.) $)
    ersymb2 $p |- ( Er R -> ( A R B <-> B R A ) ) $=
      ( wer wbr ersym2 impbid ) CFABCGBACGABCDEHBACEDHI $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y $.  $d y R $.  $d x A $.  $d x R $.
    $( Closed form of ~ erref .  (Contributed by Rodolfo Medina,
       11-Oct-2010.) $)
    erreft $p |- ( Er R -> ( A e. ( dom R u. ran R ) -> A R A ) ) $=
      ( vx vy wer cv wbr cdm crn cun wral wcel wex vex wa anidm syl5bir exlimdv
      ersym2 syl5bi wi wo elun eldm ertr2 sylan2d elrn jaod ralrimiv wceq breq1
      syland breq2 bitrd rcla4cv syl ) BEZCFZURBGZCBHZBIZJZKAVBLAABGZUAUQUSCVBU
      RVBLURUTLZURVALZUBUQUSURUTVAUCUQVDUSVEVDURDFZBGZDMUQUSDURBCNZUDUQVGUSDVGV
      GVGOUQUSVGPUQVGVFURBGZVGUSURVFBVHDNZSURVFURBVHVJVHUEZUFQRTVEVIDMUQUSDURBV
      HUGUQVIUSDVIVIVIOUQUSVIPUQVIVGVIUSVFURBVJVHSVKULQRTUHTUIUSVCCAVBURAUJUSAU
      RBGVCURAURBUKURAABUMUNUOUP $.
      $( [11-Oct-2010] $)
  $}

  $( Simplified version of ~ erreft .  (Contributed by Rodolfo Medina,
     11-Oct-2010.) $)
  erreft2 $p |- ( Er R -> ( A e. dom R -> A R A ) ) $=
    ( wer cdm wcel crn cun unidm erdmrn uneq2d syl5reqr eleq2d erreft sylbird
    wbr ) BCZABDZEAQBFZGZEAABOPSQAPQQQGSQHPQRQBIJKLABMN $.
    $( [11-Oct-2010] $)

  $( An equivalence relation is a set if its domain is a set.  (Contributed by
     Rodolfo Medina, 15-Oct-2010.) $)
  erex $p |- ( ( Rel R /\ Er R ) -> ( dom R e. A -> R e. _V ) ) $=
    ( wrel wer wa cdm crn cxp cvv wi wb erdmrn eleq1d biimpd pm4.71 sylib xpexg
    wcel syl6bi pm5.31r sylan2 wss relssdmrn ssexg sylan syl6 ) BCZBDZEBFZARZUG
    UIBGZHZIRZEZBIRZUHUGUJUMJUJUNJUHUJUJUKARZEZUMUHUJUPJUJUQKUHUJUPUHUIUKABLMNU
    JUPOPUIUKAAQSUJUMUGTUAUGBULUBUMUOBUCBULIUDUEUF $.
    $( [15-Oct-2010] $)

  $( An equivalence relation is a set if and only if its domain is a set.
     (Contributed by Rodolfo Medina, 15-Oct-2010.) $)
  erex2 $p |- ( ( Rel R /\ Er R ) -> ( R e. _V <-> dom R e. _V ) ) $=
    ( wrel wer wa cvv wcel cdm dmexg erex impbid2 ) ABACDAEFAGEFAEHEAIJ $.
    $( [15-Oct-2010] $)

  ${
    $d x A $.  $d x B $.
    $( Lemma for ~ prter3 . $)
    prtlem9 $p |- ( A e. B -> E. x e. B [ x ] R = [ A ] R ) $=
      ( wcel cv wceq wrex cec risset eceq1 reximi sylbi ) BCEAFZBGZACHNDIBDIGZA
      CHABCJOPACNBDKLM $.
      $( [25-Sep-2010] $)
  $}

  ${
    $d v z $.  $d v w $.  $d v R $.
    $( Lemma for ~ prter3 . $)
    prtlem10 $p |- ( Er R -> ( z e. dom R ->
                                ( z R w <->
                                        E. v e. dom R ( z e. [ v ] R /\
                                                        w e. [ v ] R ) ) ) ) $=
      ( wer cv cdm wcel wbr cec wa wrex erreft2 elec syl6ibr biimpri wi biimprd
      vex eleq2 jca3 wceq prtlem9 reximi r19.37av prtlem60 ersym2 syl5bi biimpi
      anbi12d 3syl prtlem50 ertr2 syld reximdv c0 wne wb ne0i r19.9rzv syl syl9
      impbidd ) DEZAFZDGZHZVEBFZDIZVECFZDJZHZVHVKHZKZCVFLZVDVGVIVEVEDJZHZVHVPHZ
      KZVOVDVGVQVIVRVDVGVEVEDIVQVEDMVEVEDASZVTNOVRVIVHVEDBSZVTNPUAVGVKVPUBZCVFL
      VSVNQZCVFLVSVOQCVEVFDUCWBWCCVFWBVNVSWBVLVQVMVRVKVPVETVKVPVHTUJRUDVSVNCVFU
      EUKUFVDVOVICVFLZVGVIVDVNVICVFVDVNVEVJDIZVJVHDIZKVIVDVLWEVMWFVLVJVEDIVDWEV
      EVJDVTCSZNVJVEDWGVTUGUHVMWFVHVJDWAWGNUIULVEVJVHDVTWGWAUMUNUOVGVIWDVGVFUPU
      QVIWDURVFVEUSVICVFUTVARVBVC $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x R $.
    $( Lemma for ~ prter2 . $)
    prtlem11 $p |- ( B e. D ->
                        ( C e. A -> ( B = [ C ] R -> B e. ( A /. R ) ) ) ) $=
      ( vx wcel cec wceq cqs wa cv wrex risset r19.41v eceq1 eqtr3 eqcomd sylan
      reximi sylbir sylanb elqsg syl5ibr exp3a ) BDGZCAGZBCEHZIZBAEJGZUGUIKUJUF
      BFLZEHZIZFAMZUGUKCIZFAMZUIUNFCANUPUIKUOUIKZFAMUNUOUIFAOUQUMFAUOULUHIZUIUM
      UKCEPURUIKULBULBUHQRSTUAUBFABEDUCUDUE $.
      $( [12-Oct-2010] $)
  $}

  ${
    $d x y $.
    $( Lemma for ~ prtex and ~ prter3 . $)
    prtlem12 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> Rel R ) $=
      ( cv wcel wa wrex copab wceq wrel relopab releq mpbiri ) EAFCFZGBFPGHCDIZ
      ABJZKELRLQABMERNO $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z $.  $d u v w $.
    $d u v A $.
    $( Lemma for ~ prter1 , ~ prter2 , ~ prter3 and ~ prtex . $)
    prtlem13 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> ( z R w <-> E. v e. A ( z e. v /\ w e. v ) ) ) $=
      ( cv wcel wa wrex copab wceq wbr wsbc brabsb2 prtlem5 syl6bb elequ2
      anbi12d cbvrexv ) HAIFIZJBIUCJKFGLZABMNZCIZDIZHOZUFUCJZUGUCJZKZFGLZUFEIZJ
      ZUGUMJZKZEGLUEUHUDAUFPBUGPULUDABCDHQFBAGDCRSUKUPFEGUCUMNUIUNUJUOFECTFEDTU
      AUBS $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u w z A $.  $d u v w z A $.  $d w z R $.
    $( Lemma for ~ prtex , ~ prter2 and ~ prter3 . $)
    prtlem16 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> dom R = U. A ) $=
      ( vz vv vw cv wcel wa wrex copab wceq cdm cuni wex exbii bitr4i syl6bb
      wbr eldm prtlem13 exbidv syl5bb df-rex anass excom exdistr exsimpl syl6bi
      exancom syl6ib eluni syl6ibr elex2 adantl ancli eximi sylbi syl5ibr eqrdv
      vex impbid ) EAICIZJBIVEJKCDLABMNZFEOZDPZVFFIZVGJZVIVHJZVFVJVIGIZJZVLDJZK
      GQZVKVFVJVNVMKZGQZVOVFVJVPHIZVLJZHQZKZGQZVQVFVJVPVSKZHQGQZWBVFVJWCGQZHQZW
      DVFVJVMVSKZGDLZHQZWFVJVIVREUAZHQVFWIHVIEFVCUBVFWJWHHABFHGCDEUCUDUEWHWEHWH
      VNWGKZGQWEWGGDUFWCWKGVNVMVSUGRSRTWCHGUHTVPVSGHUITZVPVTGUJUKVNVMGULZUMGVID
      UNZUOVKVJVFWBVKVQWBVKVOVQWNWMSVPWAGVPVTVMVTVNHVIVLUPUQURUSUTWLVAVDVB $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( Lemma for ~ prter2 and also a property of partitions . $)
    prtlem400 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> -. (/) e. ( U. A /. R ) ) $=
      ( cv wcel wa wrex copab wceq c0 cuni cqs wn eqid cdm wne wi prtlem16 syl
      elqsn0 ex necon2bd mpi ) EAFCFZGBFUFGHCDIABJKZLLKLDMZENGZOLPUGUILLUGEQUHK
      ZUILLRZSABCDETUJUIUKUHLEUBUCUAUDUE $.
      $( [15-Oct-2010] $)
  $}

  $( Introduce the partition predicate. $)
  $c Prt $.

  $( Extend the definition of a wff to include the partition predicate. $)
  wprt $a wff Prt A $.

  ${
    $d x y A $.
    $( Define the partition predicate. $)
    df-prt $a |- ( Prt A <-> A. x e. A A. y e. A ( x = y \/
                                                ( x i^i y ) = (/) ) ) $.
  $}

  ${
    $d x y A $.  $d x y R $.
    $( The quotient set of an equivalence relation is a partition.
       (Contributed by Rodolfo Medina, 13-Oct-2010.) $)
    erprt $p |- ( Er R -> Prt ( A /. R ) ) $=
      ( vx vy wer cv wceq cin c0 wo cqs wral wprt wa simpl simprl simprr qsdisj
      wcel ralrimivva df-prt sylibr ) BEZCFZDFZGUDUEHIGJZDABKZLCUGLUGMUCUFCDUGU
      GUCUDUGSZUEUGSZNZNAUDUEBUCUJOUCUHUIPUCUHUIQRTCDUGUAUB $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d w x $.  $d w y $.
    $( Lemma for ~ prter1 , ~ prter2 and ~ prtex . $)
    prtlem14 $p |- ( Prt A -> ( ( x e. A /\ y e. A )
                                -> ( ( w e. x /\ w e. y ) -> x = y ) ) ) $=
      ( wprt cv wcel wa wceq cin c0 wo wi wral df-prt ra42 sylbi elin wn wal
      eq0 ax-4 pm2.21d syl5bir prtlem1 syl6 ) DEZAFZDGBFZDGHZUHUIIZUHUIJZKIZLZC
      FZUHGUOUIGHZUKMUGUNBDNADNUJUNMABDOUNABDDPQUKUMUPUPUOULGZUMUKUOUHUIRUMUQUK
      UMUQSZCTURCULUAURCUBQUCUDUEUF $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d z x $.  $d z y $.  $d y w $.
    $( Lemma for ~ prter2 . $)
    prtlem17 $p |- ( Prt A -> ( ( x e. A /\ z e. x )
                                        -> ( E. y e. A ( z e. y /\ w e. y )
                                                -> w e. x ) ) ) $=
      ( wprt cv wcel wa wrex wex df-rex an32 wceq prtlem14 elequ2 biimprd exp4a
      wi syl8 imp3a syl5bir exp3a imp5a imp4b exlimdv syl5bi ex ) EFZAGZEHZCGZU
      JHZIZULBGZHZDGZUOHZIZBEJZUQUJHZSUTUOEHZUSIZBKUIUNIZVAUSBELVDVCVABUIUNVBUS
      VAUIUNVBUPURVAUIUNVBUPURVASZSZUNVBIUKVBIZUMIUIVFUKVBUMMUIVGUMVFUIVGUMUPVE
      UIVGUMUPIUJUONZVEABCEOVHVAURABDPQTRUAUBUCUDUEUFUGUH $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z $.  $d u v w $.
    $d u p z $.  $d u p w $.  $d u v p A $.  $d v R $.
    $( Lemma for ~ prter2 . $)
    prtlem18 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( ( v e. A /\ z e. v ) -> ( w e. v <-> z R w ) ) )
        $=
      ( vp cv wcel wa wrex copab wceq wprt wbr wb wi prtlem13 wex df-rex syl6bb
      anass wal biimprd 19.23v sylibr 19.21bi syl5bi exp3a a1d imp32 biimpd imp
      prtlem17 sylan9 impbid expr ancoms ) HAJFJZKBJVAKLFGMABNOZGPZEJZGKZCJZVDK
      ZLZDJZVDKZVFVIHQZRZSVBVCVHVLVBVCVHLZLVJVKVBVCVHVJVKSZVBVHVNSVCVBVHVJVKVHV
      JLVEVGVJLZLZVBVKVEVGVJUDVBVPVKSZEVBVPEUAZVKSVQEUEVBVKVRVBVKVOEGMVRABCDEFG
      HTVOEGUBUCUFVPVKEUGUHUIUJUKULUMVBVKVFIJZKVIVSKLIGMZVMVJVBVKVTABCDIFGHTUNV
      CVHVTVJSEICDGUPUOUQURUSUT $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u w z A $.  $d u v w z A $.  $d v w R $.
    $( Lemma for ~ prter2 . $)
    prtlem19 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( ( v e. A /\ z e. v ) -> v = [ z ] R ) ) $=
      ( vw wprt cv wcel wa wrex copab wceq cec wbr vex elec wb prtlem18 bicomdd
      imp syl5bb eqrdv eqcomd ex ) FIGAJEJZKBJUHKLEFMABNOLZDJZFKCJZUJKLZUJUKGPZ
      OUIULLZUMUJUNHUMUJHJZUMKUKUOGQZUNUOUJKZUOUKGHRCRSUIULUPUQTUIULUQUPABCHDEF
      GUAUBUCUDUEUFUG $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x z A $.  $d x y u $.  $d x y v $.  $d x z u $.
    $d x z v $.  $d w x $.  $d w y $.
    $( Lemma for ~ prter1 and ~ prtex . $)
    prtlem15 $p |- ( Prt A -> ( E. x e. A E. y e. A ( ( u e. x /\ w e. x )
                        /\ ( w e. y /\ v e. y ) )
                        -> E. z e. A ( u e. z /\ v e. z ) ) ) $=
      ( wprt cv wcel wa wrex anabs7 an43 anbi2i 3bitr4ri wceq prtlem14 elequ2
      wi an3 anbi2d syl5ibr syl8 imp4a syl7bi expdimp rexlimdv reximdva anbi12d
      cbvrexv syl6ib ) GHZFIZAIZJZDIZUOJZKUQBIZJZEIZUSJZKKZBGLZAGLUPVAUOJZKZAGL
      UNCIZJZVAVGJZKZCGLUMVDVFAGUMUOGJZKVCVFBGUMVKUSGJZVCVFTZVCURUTKZVCKZUMVKVL
      KZVFVNUPVBKZVNKZKVRVOVCVQVNMVCVRVNUPURUTVBNZOVSPUMVPVNVCVFUMVPVNUOUSQZVMA
      BDGRVCVFVTVQUPURUTVBUAVTVEVBUPABESUBUCUDUEUFUGUHUIVFVJACGUOVGQUPVHVEVIACF
      SACESUJUKUL $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u p A $.  $d u v r A $.
    $d u v w q A $.  $d u v z p q A $.  $d z w p R $.  $d u r z $.  $d u r p $.
    $( Every partition generates an equivalence relation.  (Contributed by
       Rodolfo Medina, 13-Oct-2010.) $)
    prter1 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> Er R ) $=
      ( vz vw vp vv vr vq cv wcel wa wrex wbr wi wal prtlem13 biimpd copab wceq
      wprt wer ancom rexbii syl6bb bitr4d adantr anbi12d reeanv prtlem15 sylan9
      syl6bbr wb sylibrd jca alrimivv alrimiv dfer2 sylibr ancoms ) EALCLZMBLVC
      MNCDOABUAUBZDUCZEUDZVDVENZFLZGLZEPZVIVHEPZQZVJVIHLZEPZNZVHVMEPZQZNZHRGRZF
      RVFVGVSFVGVRGHVGVLVQVDVLVEVDVJVKVDVJVIILZMZVHVTMZNZIDOZVKVDVJWBWANZIDOZWD
      ABFGICDESZWEWCIDWBWAUEUFUGABGFICDESUHTUIVGVOVHJLZMVMWHMNJDOZVPVDVOWEVIKLZ
      MVMWJMNZNKDOIDOZVEWIVDVOWLVDVOWFWKKDOZNWLVDVJWFVNWMWGABGHKCDESUJWEWKIKDDU
      KUNTIKJGHFDULUMVDVPWIUOVEABFHJCDESUIUPUQURUSFGHEUTVAVB $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( Lemma for ~ prtex . $)
    prtlem300 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( R e. _V <-> U. A e. _V ) ) $=
      ( wprt cv wcel wa wrex copab wceq cvv cdm cuni wrel wer prtlem12 adantl
      wb prter1 erex2 syl2anc prtlem16 eleq1d bitrd ) DFZEAGCGZHBGUHHICDJABKLZI
      ZEMHZENZMHZDOZMHZUJEPZEQUKUMTUIUPUGABCDERSABCDEUAEUBUCUIUMUOTUGUIULUNMABC
      DEUDUESUF $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( The equivalence relation generated by a partition is a set if and only
       if the partition itself is a set.  (Contributed by Rodolfo Medina,
       15-Oct-2010.) $)
    prtex $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( R e. _V <-> A e. _V ) ) $=
      ( wprt cv wcel wa wrex copab wceq cvv cuni prtlem300 uniexb syl6bbr ) DFE
      AGCGZHBGRHICDJABKLIEMHDNMHDMHABCDEODPQ $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u v z p A $.  $d v z p R $.
    $( The quotient set of the equivalence relation generated by a partition
       equals the partition itself.  (Contributed by Rodolfo Medina,
       17-Oct-2010.) $)
    prter2 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( U. A /. R ) = ( A \ { (/) } ) ) $=
      ( vp vv vz cv wcel wa wrex wceq c0 wex df-rex wral wi reximi cvv wprt cqs
      copab cuni csn cdif wne cec rexcom4 r19.41v exbii bitri rexbii vex eluni2
      elqs anbi1i 3bitri 3bitr4ri prtlem19 ralrimivv 2r19.29 ex syl syl5bi syl6
      eqtr3 19.41v simprbi risset syl6ibr wn prtlem400 prtlem90 adantl jcad 0ex
      eldifsn prtlem80 ax-mp n0el mpbi rspec eldifi jca ancomsd elunii prtlem11
      jca2r imp eximdv 19.9v 3imtr3g syl5 impbid eqrdv ) DUAZEAICIZJBIWRJKCDLAB
      UCMZKZFDUDZEUBZDNUEZUFZWTFIZXBJZXEXDJZWTXFXEDJZXENUGZKXGWTXFXHXIWTXFGIZXE
      MZGDLZXHWTXFXKHXJLZGDLZXLWTXFXJHIZEUHZMZXEXPMZKZHXJLZGDLZXNXFXRHXJLZGDLZW
      TYAXOXJJZXRKZHOZGDLZYDGDLZXRKZHOZYCXFYGYEGDLZHOYJYEGHDUIYKYIHYDXRGDUJUKUL
      YBYFGDXRHXJPUMXFXRHXALXOXAJZXRKZHOYJHXAXEEFUNZUPXRHXAPYMYIHYLYHXRGXODUOUQ
      UKURUSWTXQHXJQGDQZYCYARWTXQGHDXJABHGCDEUTVAYOYCYAXQXRGHDXJVBVCVDVEXTXMGDX
      SXKHXJXJXEXPVGSSVFXMXKGDXMYDHOZXKXMYDXKKHOYPXKKXKHXJPYDXKHVHULVISVFGXEDVJ
      VKWSXFXIRZWQWSNXBJVLYQABCDEVMNXBXEVNVDVOVPXEDNVRVKXGXOXEJZHOZXHKZWTXFXGYS
      XHYSFXDNXDJVLZYSFXDQNTJUUAVQNTDVSVTFHXDWAWBWCXEDXCWDWEWTYRXHKZHOXFHOYTXFW
      TUUBXFHWTUUBYMXFWTUUBXRYLWTXHYRXRABHFCDEUTWFXOXEDWGWIYLXRXFXETJYLXRXFRRYN
      XAXEXOTEWHVTWJVFWKYRXHHVHXFHWLWMWNWOWP $.
      $( [17-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z A $.  $d u v w A $.
    $d u v S $.  $d z w R $.  $d z w t S $.  $d t v $.  $d t A $.
    $( For every partition there exists a unique equivalence relation whose
       quotient set equals the partition.  (Contributed by Rodolfo Medina,
       19-Oct-2010.) $)
    prter3 $p |- ( ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } /\
                        ( Rel S /\
                                ( Er S /\
                                        ( S e. _V /\
                                                ( dom R = dom S /\
                                                        ( U. A /. S ) =
                                                        ( A \ { (/) } )
                        ) ) ) ) ) -> R = S ) $=
      ( vz vw vv vt cv wcel wa wrex wceq cvv wb eleq2 wex sylan9bb wrel wer cdm
      copab cuni cqs c0 csn cdif an12 anbi2i anabs5 bitri prtlem12 wbr ad2antrl
      vex breldm ad2antll prtlem70 cec prtlem10 imp prtlem16 eqeq1 syl5ib rexeq
      syl6com exbii rexcom4 ecexg anbi12d ceqsexgv syl rexbidv syl5bbr syl5rbbr
      df-rex elqs anbi1i 19.41v anass 3bitr2ri syl6bb prtlem100 prtlem13 bicomd
      syl6bbr sylbir bicomdd sylbid syl5 ibd ibdr impbid adantl eqbrrdv2 anasss
      ex sylan ) EAKCKZLBKXALMCDNABUDOZFUAZFUBZFPLZEUCZFUCZOZDUEZFUFZDUGUHUIZOZ
      MMZMZMZMZXBXCXBXNMZMZMZEFOZXSXBXPMXPXRXPXBXCXBXNUJUKXBXOULUMXBEUAZXRXTABC
      DEUNYAXCXQXTXQGHEFXQGKZHKZEUOZYBYCFUOZQZYAXCMXQYDYEXQYDYEYDYBXFLZXQYFYBYC
      EGUQZURXQYGYBXGLZYFXMYGYIQZXBXDXHYJXEXLXFXGYBRUPUSXQYIYEYDXQYIYEYDQZXQYIM
      XDYIMZXBXHMZXEXLMZMZMZXBMYKXBXDXEXHXLYIUTYPYEYBIKZLZYCYQLZMZIDNZXBYDYLYEY
      BJKZFVAZLZYCUUCLZMZJXGNZYOUUAXDYIYEUUGQGHJFVBVCYMUUGUUFJXINZYNUUAXBXHUUGU
      UHQZXHXBXGXIOZUUIXBXFXIOXHUUJABCDEVDXFXGXIVEVFUUFJXGXIVGVHVCYNUUHYTIXKNZU
      UAXEUUHYTIXJNZXLUUKXEUUHYQXJLZYTMZISZUULXEUUHUUBXILZYQUUCOZYTMZMZJSZISZUU
      OUVAUURJXINZISZXEUUHUVBUUTIUURJXIVRVIUVCUURISZJXINXEUUHUURJIXIVJXEUVDUUFJ
      XIXEUUCPLUVDUUFQUUBPFVKYTUUFIUUCPUUQYRUUDYSUUEYQUUCYBRYQUUCYCRVLVMVNVOVPV
      QUUTUUNIUUNUUPUUQMZJSZYTMUVEYTMZJSUUTUUMUVFYTUUMUUQJXINUVFJXIYQFIUQVSUUQJ
      XIVRUMVTUVEYTJWAUVGUUSJUUPUUQYTWBVIWCVIWDYTIXJVRWHYTIXJXKVGTYSIDYBWEWHTTX
      BYDUUAABGHICDEWFWGTWIWSWJZWKWLWMXQYDYEYEYIXQYFYBYCFYHURUVHWLWNWOWPWQWRWTW
      I $.
      $( [19-Oct-2010] $)
  $}

$( (End of Rodolfo Medina's mathbox.) $)


