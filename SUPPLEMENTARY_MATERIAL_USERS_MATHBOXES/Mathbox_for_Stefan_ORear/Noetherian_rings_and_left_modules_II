$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Noetherian rings and left modules II
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LNoeR $.

  $( Extend class notation with the class of left Noetherian rings. $)
  clnr $a class LNoeR $.

  $( A ring is _left-Noetherian_ iff it is Noetherian as a left module over
     itself. $)
  df-lnr $a |- LNoeR = { a e. Ring | ( ringLMod ` a ) e. LNoeM } $.

  ${
    $d A a $.
    $( Property of a left-Noetherian ring.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    islnr $p |- ( A e. LNoeR <-> ( A e. Ring /\
        ( ringLMod ` A ) e. LNoeM ) ) $=
      ( va cv crglmod cfv clnm wcel crg clnr wceq fveq2 eleq1d df-lnr elrab2 )
      BCZDEZFGADEZFGBAHIOAJPQFOADKLBMN $.
      $( [24-Jan-2015] $)

    $( Left-Noetherian rings are rings.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    lnrrng $p |- ( A e. LNoeR -> A e. Ring ) $=
      ( clnr wcel crg crglmod cfv clnm islnr simplbi ) ABCADCAEFGCAHI $.
      $( [24-Jan-2015] $)

    $( Left-Noetherian rings have Noetherian associated modules.  (Contributed
       by Stefan O'Rear, 24-Jan-2015.) $)
    lnrlnm $p |- ( A e. LNoeR -> ( ringLMod ` A ) e. LNoeM ) $=
      ( clnr wcel crg crglmod cfv clnm islnr simprbi ) ABCADCAEFGCAHI $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d i g R $.  $d i g N $.  $d i g U $.  $d i g B $.
    islnr2.b $e |- B = ( Base ` R ) $.
    islnr2.u $e |- U = ( LIdeal ` R ) $.
    islnr2.n $e |- N = ( RSpan ` R ) $.
    $( Property of being a left-Noetherian ring in terms of finite generation
       of ideals (the usual "pure ring theory" definition).  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    islnr2 $p |- ( R e. LNoeR <-> ( R e. Ring /\
        A. i e. U E. g e. ( ~P B i^i Fin ) i = ( N ` g ) ) ) $=
      ( wcel crg cfv wa cv wceq cpw cfn cin eqid syl5eq clnr crglmod clnm islnr
      wrex wral clspn cbs clmod rlmlmod biantrurd islnm2 syl6rbbr clidl lidlval
      clss rlmbase pweqd ineq1d rspval fveq1d eqeq2d rexeqbidv raleqbidv bitr4d
      crsp pm5.32i bitri ) BUAJBKJZBUBLZUCJZMVIENZDNZFLZOZDAPZQRZUEZECUFZMBUDVI
      VKVSVIVKVLVMVJUGLZLZOZDVJUHLZPZQRZUEZEVJUPLZUFZVSVIWHVJUIJZWHMVKVIWIWHBUJ
      UKWCWGDEVJVTWCSWGSVTSULUMVIVRWFECWGVICBUNLWGHBKUOTVIVOWBDVQWEVIVPWDQVIAWC
      VIABUHLWCGBKUQTURUSVIVNWAVLVIVMFVTVIFBVFLVTIBKUTTVAVBVCVDVEVGVH $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d a b c R $.
    $( Left principal ideal rings are left Noetherian.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    lpirlnr $p |- ( R e. LPIR -> R e. LNoeR ) $=
      ( va vb vc clpir wcel crg cv crsp cfv wceq cbs cpw cfn cin wrex wral eqid
      wa sylanbrc clidl lpirrng clpidl csn wb islpidl syl biimpa snelpwi adantl
      clnr snfi fveq2 eqeq2d rcla4ev sylancl eqeq1 rexbidv syl5ibrcom rexlimdva
      a1i elin mpd ralrimiva islpir simprbi raleqdv mpbird islnr2 ) AEFZAGFZBHZ
      CHZAIJZJZKZCALJZMZNOZPZBAUAJZQZAUKFAUBZVJWBVTBAUCJZQVJVTBWDVJVLWDFZSZVLDH
      ZUDZVNJZKZDVQPZVTVJWEWKVJVKWEWKUEWCVQWDADVLVNWDRZVNRZVQRZUFUGUHWFWJVTDVQW
      FWGVQFZSZVTWJWIVOKZCVSPZWPWHVSFZWIWIKZWRWPWHVRFZWHNFZWSWOXAWFWGVQUIUJXBWP
      WGULVAWHVRNVBTWIRWQWTCWHVSVMWHKVOWIWIVMWHVNUMUNUOUPWJVPWQCVSVLWIVOUQURUSU
      TVCVDVJVTBWAWDVJVKWAWDKWDAWAWLWARZVEVFVGVHVQAWACBVNWNXCWMVIT $.
      $( [24-Jan-2015] $)
  $}

  ${
    lnrfrlm.y $e |- Y = ( R freeLMod I ) $.
    $( Finite-dimensional free modules over a Noetherian ring are Noetherian.
       (Contributed by Stefan O'Rear, 3-Feb-2015.) $)
    lnrfrlm $p |- ( ( R e. LNoeR /\ I e. Fin ) -> Y e. LNoeM ) $=
      ( clnr wcel cfn wa crglmod cfv cpws co clnm frlmpwsfi lnrlnm pwslnm sylan
      eqid eqeltrd ) AEFZBGFZHCAIJZBKLZMACBEDNTUBMFUAUCMFAOBUBUCUCRPQS $.
      $( [3-Feb-2015] $)
  $}

  ${
    $d S a b c d $.  $d M a b c d $.
    lnrfg.s $e |- S = ( Scalar ` M ) $.
    $( Finitely-generated modules over a Noetherian ring, being homomorphic
       images of free modules, are Noetherian.  (Contributed by Stefan O'Rear,
       7-Feb-2015.) $)
    lnrfg $p |- ( ( M e. LFinGen /\ S e. LNoeR ) -> M e. LNoeM ) $=
      ( va vb clfig wcel clnr wa cv cfv cbs wceq clnm eqid co crn cvv a1i wf wb
      cfn clspn cpw wrex clmod fglmod islmodfg syl ibi cfrlm cid cres cvsca cof
      cgsu cmpt clmhm ad3antrrr vex csca wss wf1o f1oi f1of ax-mp elpwi sylancr
      adantr fss ad2antlr frlmup1 simplll simpllr simprl lnrfrlm syl2anc rnresi
      frlmup3 fveq2i simprr syl5eq eqtrd lnmepi syl22anc exp31 rexlimdv mpd ) B
      FGZAHGZIZDJZUBGZWLBUCKZKZBLKZMZIZDWPUDZUEZBNGZWIWTWJWIWTWIBUFGZWIWTUABUGZ
      WPWNBDWPOZWNOZUHUIUJVIWKWRXADWSWKWLWSGZWRXAWKXFIZWRIZEAWLUKPZLKZBEJULWLUM
      ZBUNKZUOPUPPUQZXIBURPGWIXINGZXMQZWPMXAXHEXKXJWPABXMXIWLXLRXIOZXJOZXDXLOZX
      MOZWIXBWJXFWRXCUSZWLRGXHDUTSZABVAKMXHCSZXFWLWPXKTZWKWRXFWLWLXKTZWLWPVBYCW
      LWLXKVCYDWLVDWLWLXKVEVFWLWPVGWLWLWPXKVJVHVKZVLWIWJXFWRVMXHWJWMXNWIWJXFWRV
      NXGWMWQVOAWLXIXPVPVQXHXOXKQZWNKZWPXHEXKXJWPABXMXIWLWNXLRXPXQXDXRXSXTYAYBY
      EXEVSXHYGWOWPYFWLWNWLVRVTXGWMWQWAWBWCWPXIBXMFXDWDWEWFWGWH $.
      $( [7-Feb-2015] $)

    lnrfgtr.u $e |- U = ( LSubSp ` M ) $.
    lnrfgtr.n $e |- N = ( M |`s P ) $.
    $( A submodule of a finitely generated module over a Noetherian ring is
       finitely generated.  Often taken as the definition of Noetherian ring.
       (Contributed by Stefan O'Rear, 7-Feb-2015.) $)
    lnrfgtr $p |- ( ( M e. LFinGen /\ S e. LNoeR /\ P e. U ) ->
        N e. LFinGen ) $=
      ( clfig wcel clnr w3a clnm lnrfg 3adant3 simp3 lnmlssfg syl2anc ) DIJZBKJ
      ZACJZLDMJZUAEIJSTUBUABDFNOSTUAPECADGHQR $.
      $( [7-Feb-2015] $)
  $}

