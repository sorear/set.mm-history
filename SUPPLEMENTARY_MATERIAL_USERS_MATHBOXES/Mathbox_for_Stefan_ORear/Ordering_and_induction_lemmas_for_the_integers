$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Ordering and induction lemmas for the integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a b x y $.  $d B a b x y $.  $d C a b c d y $.  $d D a x y $.
    $d E a x y $.  $d F b x $.  $d G b x $.  $d H a b c d x y $.
    $d ph a b c d x y $.
    monotuz.1 $e |- ( ( ph /\ y e. H ) -> F < G ) $.
    monotuz.2 $e |- ( ( ph /\ x e. H ) -> C e. RR ) $.
    monotuz.3 $e |- H = ( ZZ>= ` I ) $.
    monotuz.4 $e |- ( x = ( y + 1 ) -> C = G ) $.
    monotuz.5 $e |- ( x = y -> C = F ) $.
    monotuz.6 $e |- ( x = A -> C = D ) $.
    monotuz.7 $e |- ( x = B -> C = E ) $.
    $( A function defined on a set of upper integers which increases at every
       adjacent pair is globally strictly monotonic by induction.  (Contributed
       by Stefan O'Rear, 24-Sep-2014.) $)
    monotuz $p |- ( ( ph /\ ( A e. H /\ B e. H ) ) -> ( A < B <-> D < E ) ) $=
      ( wcel va vb vc vd wa clt wbr csb cv csbeq1 cuz cr cz uzssz zssre eqsstri
      cfv sstri ax-17 ax17el hbcsb1 hbel hbim weq anbi2d csbeq1a eleq1d imbi12d
      wi eleq1 chvar simpl adantlrr simplrl sseldi simplrr simpr c1 wceq breq2d
      caddc co imbi2d csbief syl5eqr ovex oveq1 csbeq1d breq12d vtoclg 3ad2ant2
      vex w3a simp2l cle simp3 zre 3ad2ant1 ltle syl2anc mpd simp11 simp12 eluz
      mpbird simp2r syl6eleq uztrn syl6eleqr peano2uz eleq2i biimpri 3syl lttrd
      wb vtoclf 3exp a2d uzind2 syl3anc ex ltord1 a17d csbiegf breqan12d adantl
      bitrd ) ADKTZEKTZUEZUEDEUFUGBDFUHZBEFUHZUFUGZGHUFUGZAUAUBBUAUIZFUHZBUBUIZ
      FUHZDEKYKYLBYOYQFUJBYODFUJBYOEFUJKLUKUQZULOYSUMULLUNZUOURUPABUIZKTZUEZFUL
      TZVIZAYOKTZUEZYPULTZVIZBUAUUGUUHBUUGBUSZBUCUCYPULBUCYOFUCUABUTVAUCUIZULTB
      USVBVCBUAVDZUUCUUGUUDUUHUULUUBUUFAUUAYOKVJVEUULFYPULBYOFVFVGVHZNVKAUUFYQK
      TZUEUEZYOYQUFUGZYPYRUFUGZUUOUUPUEZUUGUUQAUUFUUPUUGUUNUUGUUPVLVMUURYOUMTZY
      QUMTUUPUUGUUQVIZUURKUMYOKYSUMOYTUPZAUUFUUNUUPVNVOUURKUMYQUVAAUUFUUNUUPVPV
      OUUOUUPVQUUGYPBUUKFUHZUFUGZVIUUGYPBYOVRWAWBZFUHZUFUGZVIZUUGYPBUDUIZFUHZUF
      UGZVIUUGYPBUVHVRWAWBZFUHZUFUGZVIUUTUCUDYOYQUUKUVDVSZUVCUVFUUGUVNUVBUVEYPU
      FBUUKUVDFUJVTWCUCUDVDZUVCUVJUUGUVOUVBUVIYPUFBUUKUVHFUJVTWCUUKUVKVSZUVCUVM
      UUGUVPUVBUVLYPUFBUUKUVKFUJVTWCUCUBVDZUVCUUQUUGUVQUVBYRYPUFBUUKYQFUJVTWCAC
      UIZKTZUEZIJUFUGZVIZUVGCYOUMCUAVDZUVTUUGUWAUVFUWCUVSUUFAUVRYOKVJVEUWCIYPJU
      VEUFUWCIBUVRFUHZYPBUBUVRFICWLYQITBUSQWDZBUVRYOFUJWEUWCJBUVRVRWAWBZFUHZUVE
      BUBUWFFJUVRVRWAWFYQJTBUSPWDZUWCBUWFUVDFUVRYOVRWAWGWHWEWIVHMWJUUSUVHUMTZYO
      UVHUFUGZWMZUUGUVJUVMUWKUUGUVJUVMUWKUUGUVJWMZYPUVIUVLUUGUWKUUHUVJUUEUUIBUA
      UUGUUHBUUJBUBUBYPULBUBYOFUBUABUTVAYQULTBUSZVBVCUUMNVKWKUWLAUVHKTZUVIULTZU
      WKAUUFUVJWNZUWLUVHYSKUWLUVHYOUKUQTZYOYSTUVHYSTZUWLUWQYOUVHWOUGZUWKUUGUWSU
      VJUWKUWJUWSUUSUWIUWJWPUWKYOULTZUVHULTZUWJUWSVIUUSUWIUWTUWJYOWQWRUWIUUSUXA
      UWJUVHWQWKYOUVHWSWTXAWRUWLUUSUWIUWQUWSXOUUSUWIUWJUUGUVJXBUUSUWIUWJUUGUVJX
      CYOUVHXDWTXEUWLYOKYSUWKAUUFUVJXFOXGYOUVHLXHWTZOXIZUUEAUWNUEZUWOVIBUDUXDUW
      OBUXDBUSBUBUBUVIULBUBUVHFUBUDBUTVAUWMVBVCBUDVDZUUCUXDUUDUWOUXEUUBUWNAUUAU
      VHKVJVEUXEFUVIULBUVHFVFVGVHNVKWTUWLAUVKKTZUVLULTZUWPUWLUWRUVKYSTZUXFUXBLU
      VHXJUXFUXHKYSUVKOXKXLXMUUEAUXFUEZUXGVIBUVKUXIUXGBUXIBUSBUBUBUVLULBUBUVKFY
      QUVKTBUSVAUWMVBVCUVHVRWAWFUUAUVKVSZUUCUXIUUDUXGUXJUUBUXFAUUAUVKKVJVEUXJFU
      VLULBUVKFVFVGVHNXPWTUWKUUGUVJWPUWLAUWNUVIUVLUFUGZUWPUXCUWBUXDUXKVIZCUDUXL
      CUSCUDVDZUVTUXDUWAUXKUXMUVSUWNAUVRUVHKVJVEUXMIUVIJUVLUFUXMIUWDUVIUWEBUVRU
      VHFUJWEUXMJUWGUVLUWHUXMBUWFUVKFUVRUVHVRWAWGWHWEWIVHMVKWTXNXQXRXSXTXAYAYBY
      JYMYNXOAYHYIYKGYLHUFBUADFGKYHYOGTBYCRYDBUAEFHKYIYOHTBYCSYDYEYFYG $.
      $( [24-Sep-2014] $)
  $}

  ${
    $d ph a b x y $.  $d A a b x y $.  $d B a b x y $.  $d F a b x y $.
    monotoddzzfi.1 $e |- ( ( ph /\ x e. ZZ ) -> ( F ` x ) e. RR ) $.
    monotoddzzfi.2 $e |- ( ( ph /\ x e. ZZ ) -> ( F ` -u x ) = -u ( F ` x ) )
        $.
    monotoddzzfi.3 $e |- ( ( ph /\ x e. NN0 /\ y e. NN0 ) -> ( x < y -> ( F ` x
        ) < ( F ` y ) ) ) $.
    $( A function which is odd and monotonic on ` NN0 ` is monotonic on
       ` ZZ ` .  This proof is far too long.  (Contributed by Stefan O'Rear,
       25-Sep-2014.) $)
    monotoddzzfi $p |- ( ( ph /\ A e. ZZ /\ B e. ZZ ) -> ( A < B <-> ( F ` A )
        < ( F ` B ) ) ) $=
      ( cz wcel clt wbr wa cr wi eleq1d imbi12d cn0 cc0 va vb wb cv fveq2 zssre
      cfv weq eleq1 anbi2d chvarv cn cneg wo elznn simprbi anim12i adantl nnnn0
      simpll ad2antrl ad2antll w3a simpl simpr 3anbi23d breq12 breqan12d vtocl2
      vex syl3anc ex adantrr adantr 0re a1i adantrl cle wceq biimpi neg0 fveq2i
      elnn0 0z elexi negeq fveq2d negeqd eqeq12d vtocl mpan2 cc recnd eqneg syl
      syl5eqr mpbid ad2antrr nngt0 simplll 0nn0 simplrl breq12d eqbrtrrd znegcl
      negex syl2anc ltle nn0ge0i syl5breqr breq2d mpbird jaodan breqtrd le0neg1
      mpd mpdan lelttrd a1d wn simp3 c1 zre ad2antlr 1re nnre nn0ge0 1nn0 letrd
      nnge1 lenlt 3adant3 pm2.24 sylc 3exp 3com23 3expb adantlr sylibd ltneg
      3imtr4d ccased ltord1 3impb ) ADJKEJKDELMDFUGZEFUGZLMUCAUAUBUAUDZFUGZUBUD
      ZFUGZDEJUUEUUFUUGUUIFUEUUGDFUEUUGEFUEUFABUDZJKZNZUUKFUGZOKZPZAUUGJKZNZUUH
      OKZPBUABUAUHZUUMUURUUOUUSUUTUULUUQAUUKUUGJUIUJZUUTUUNUUHOUUKUUGFUEZQRGUKZ
      AUUQUUIJKZNZNZUUGULKZUUGUMZSKZUNZUUIULKZUUIUMZSKZUNZNZUUGUUILMZUUHUUJLMZP
      ZUVEUVOAUUQUVJUVDUVNUUQUUGOKZUVJUUGUOUPUVDUUIOKZUVNUUIUOUPUQURUVFUVGUVKUV
      IUVMUVRUVFUVGUVKNZUVRUVFUWANAUUGSKZUUISKZUVRAUVEUWAUTUVGUWBUVFUVKUUGUSVAU
      VKUWCUVFUVGUUIUSZVBAUUKSKZCUDZSKZVCZUUKUWFLMZUUNUWFFUGZLMZPZPZAUWBUWCVCZU
      VRPBCUUGUUIUAVJUBVJZUUTCUBUHZNZUWHUWNUWLUVRUWQUWEUWBUWGUWCAUWQUUKUUGSUUTU
      WPVDQUWQUWFUUISUUTUWPVEQVFUWQUWIUVPUWKUVQUUKUUGUWFUUILVGUUTUWPUUNUUHUWJUU
      JLUVBUWFUUIFUEZVHRRIVIVKVLUVFUVIUVKNZUVRUVFUWSNZUVQUVPUWTUUHTUUJUVFUUSUWS
      AUUQUUSUVDUVCVMZVNZTOKZUWTVOVPUVFUUJOKZUWSAUVDUXDUUQUUPAUVDNZUXDPBUBBUBUH
      ZUUMUXEUUOUXDUXFUULUVDAUUKUUIJUIUJZUXFUUNUUJOUUKUUIFUEZQRGUKVQZVNUWTUUHTV
      RMZTUUHUMZVRMZUWTTUVHFUGZUXKVRUWTUVHULKZUVHTVSZUNZTUXMVRMZUVIUXPUVFUVKUVI
      UXPUVHWCVTVAUWTUXNUXQUXOUWTUXNNZTUXMLMZUXQUXRTFUGZTUXMLUVFUXTTVSZUWSUXNAU
      YAUVEAUXTUXTUMZVSZUYAAUXTTUMZFUGZUYBUYDTFWAWBATJKZUYEUYBVSZWDUUMUUKUMZFUG
      ZUUNUMZVSZPZAUYFNZUYGPBTTOVOWEZUUKTVSZUUMUYMUYKUYGUYOUULUYFAUUKTJUIUJZUYO
      UYIUYEUYJUYBUYOUYHUYDFUUKTWFWGUYOUUNUXTUUKTFUEZWHWIRHWJWKWPAUXTWLKUYCUYAU
      CAUXTAUYFUXTOKZWDUUPUYMUYRPBTUYNUYOUUMUYMUUOUYRUYPUYOUUNUXTOUYQQRGWJWKWMU
      XTWNWOWQVNZWRUXRTUVHLMZUXTUXMLMZUXNUYTUWTUVHWSURUXRATSKZUVIUYTVUAPZAUVEUW
      SUXNWTVUBUXRXAVPUVFUVIUVKUXNXBUWMAVUBUVIVCZVUCPBCTUVHUYNUUGXFZUYOUWFUVHVS
      ZNZUWHVUDUWLVUCVUGUWEVUBUWGUVIAVUGUUKTSUYOVUFVDZQVUGUWFUVHSUYOVUFVEZQVFVU
      GUWIUYTUWKVUAUUKTUWFUVHLVGVUGUUNUXTUWJUXMLVUGUUKTFVUHWGVUGUWFUVHFVUIWGXCR
      RIVIVKXPXDUXRUXCUXMOKZUXSUXQPUXCUXRVOVPUVFVUJUWSUXNUVFAUVHJKZVUJAUVEVDUUQ
      VUKAUVDUUGXEVAUUPAVUKNZVUJPBUVHVUEUUKUVHVSZUUMVULUUOVUJVUMUULVUKAUUKUVHJU
      IUJVUMUUNUXMOUUKUVHFUEQRGWJXGWRTUXMXHXGXPUWTUXONZUXQTUXTVRMZVUNTTUXTVRTXA
      XIUVFUYAUWSUXOUYSWRXJUXOUXQVUOUCUWTUXOUXMUXTTVRUVHTFUEXKURXLXMXQUVFUXMUXK
      VSZUWSAUUQVUPUVDUYLUURVUPPBUAUUTUUMUURUYKVUPUVAUUTUYIUXMUYJUXKUUTUYHUVHFU
      UKUUGWFWGUUTUUNUUHUVBWHWIRHUKVMZVNXNUWTUUSUXJUXLUCUXBUUHXOWOXLUWTUXTTUUJL
      UVFUYAUWSUYSVNUWTTUUILMZUXTUUJLMZUVKVURUVFUVIUUIWSVBUWTAVUBUWCVURVUSPZAUV
      EUWSUTVUBUWTXAVPUVKUWCUVFUVIUWDVBUWMAVUBUWCVCZVUTPBCTUUIUYNUWOUYOUWPNZUWH
      VVAUWLVUTVVBUWEVUBUWGUWCAVVBUUKTSUYOUWPVDQVVBUWFUUISUYOUWPVEQVFVVBUWIVURU
      WKVUSUUKTUWFUUILVGUYOUWPUUNUXTUWJUUJLUYQUWRVHRRIVIVKXPXDXRXSVLUVFUVGUVMNZ
      UVPUVQUVFVVCUVPVCUVPUVPXTZUVQUVFVVCUVPYAUVFVVCVVDUVPUVFVVCNZUUIUUGVRMZVVD
      VVEUUIYBUUGUVEUVTAVVCUVDUVTUUQUUIYCURZYDZYBOKVVEYEVPZUVGUVSUVFUVMUUGYFVAZ
      VVEUUITYBVVHUXCVVEVOVPVVIVVEUUITVRMZTUVLVRMZUVMVVLUVFUVGUVLYGVBVVEUVTVVKV
      VLUCVVHUUIXOWOXLTYBVRMVVEYBYHXIVPYIUVGYBUUGVRMUVFUVMUUGYJVAYIVVEUVTUVSVVF
      VVDUCVVHVVJUUIUUGYKXGWQYLUVPUVQYMYNYOUVFUVIUVMNZUVRUVFVVMNZUVLUVHLMZUUJUM
      ZUXKLMZUVPUVQVVNVVOUVLFUGZUXMLMZVVQAVVMVVOVVSPZUVEAUVIUVMVVTAUVMUVIVVTUWM
      AUVMUVIVCZVVTPBCUVLUVHUUIXFVUEUUKUVLVSZVUFNZUWHVWAUWLVVTVWCUWEUVMUWGUVIAV
      WCUUKUVLSVWBVUFVDQVWCUWFUVHSVWBVUFVEQVFVWCUWIVVOUWKVVSUUKUVLUWFUVHLVGVWBV
      UFUUNVVRUWJUXMLUUKUVLFUEUWFUVHFUEVHRRIVIYPYQYRVVNVVRVVPUXMUXKLUVFVVRVVPVS
      ZVVMAUVDVWDUUQUYLUXEVWDPBUBUXFUUMUXEUYKVWDUXGUXFUYIVVRUYJVVPUXFUYHUVLFUUK
      UUIWFWGUXFUUNUUJUXHWHWIRHUKVQVNUVFVUPVVMVUQVNXCYSVVNUVSUVTUVPVVOUCUVFUVSV
      VMUUQUVSAUVDUUGYCVAVNUVEUVTAVVMVVGYDUUGUUIYTXGVVNUUSUXDUVQVVQUCUVFUUSVVMU
      XAVNUVFUXDVVMUXIVNUUHUUJYTXGUUAVLUUBXPUUCUUD $.
      $( [25-Sep-2014] $)
  $}

  ${
    $d ph a b x y $.  $d A a b x y $.  $d B a b x y $.  $d E a b y $.
    $d C a b x y $.  $d D a b x y $.  $d F a b x $.  $d G a b x $.
    monotoddzz.1 $e |- ( ( ph /\ x e. NN0 /\ y e. NN0 ) -> ( x < y -> E < F ) )
        $.
    monotoddzz.2 $e |- ( ( ph /\ x e. ZZ ) -> E e. RR ) $.
    monotoddzz.3 $e |- ( ( ph /\ y e. ZZ ) -> G = -u F ) $.
    monotoddzz.4 $e |- ( x = A -> E = C ) $.
    monotoddzz.5 $e |- ( x = B -> E = D ) $.
    monotoddzz.6 $e |- ( x = y -> E = F ) $.
    monotoddzz.7 $e |- ( x = -u y -> E = G ) $.
    $( A function (given implicitly) which is odd and monotonic on ` NN0 ` is
       monotonic on ` ZZ ` .  This proof is far too long.  (Contributed by
       Stefan O'Rear, 25-Sep-2014.) $)
    monotoddzz $p |- ( ( ph /\ A e. ZZ /\ B e. ZZ ) -> ( A < B <-> C < D ) ) $=
      ( cz wcel cr va vb w3a clt wbr cmpt cfv cv wa wi ax-17 hbmpt1 ax17el hbfv
      hbel hbim weq eleq1 anbi2d fveq2 eleq1d imbi12d wceq simpr fvmpt2 syl2anc
      eqid eqeltrd chvar negeq fveq2d negeqd eqeq12d znegcl adantl negex sylan2
      cneg vtocl fvmptg chvarv 3eqtr4d hbbr 3anbi2d breq1 breq1d 3anbi3d breq2d
      breq2 nn0z 3adant3 hbeq 3adant2 breq12d sylibrd monotoddzzfi simp2 vtoclg
      cn0 anabsi7 simp3 bitrd ) ADRSZERSZUCZDEUDUEDBRHUFZUGZEXFUGZUDUEFGUDUEAUA
      UBDEXFABUHZRSZUIZXIXFUGZTSZUJAUAUHZRSZUIZXNXFUGZTSZUJBUAXPXRBXPBUKBUBUBXQ
      TBUBXNXFBUBRHULUBUABUMUNUBUHZTSBUKUOUPBUAUQZXKXPXMXRXTXJXOAXIXNRURUSXTXLX
      QTXIXNXFUTZVAVBXKXLHTXKXJHTSZXLHVCZAXJVDLBRHTXFXFVGZVEVFZLVHVIACUHZRSZUIZ
      YFVRZXFUGZYFXFUGZVRZVCZUJXPXNVRZXFUGZXQVRZVCZUJCUACUAUQZYHXPYMYQYRYGXOAYF
      XNRURUSYRYJYOYLYPYRYIYNXFYFXNVJVKYRYKXQYFXNXFUTVLVMVBYHJIVRYJYLMYHYIRSZJT
      SZYJJVCYGYSAYFVNZVOYGAYSYTUUAXKYBUJZAYSUIZYTUJBYIYFVPXIYIVCZXKUUCYBYTUUDX
      JYSAXIYIRURUSUUDHJTQVAVBLVSVQBYIHJRTXFQYDVTVFYHYKIYHYGITSZYKIVCZAYGVDUUBY
      HUUEUJBCBCUQZXKYHYBUUEUUGXJYGAXIYFRURUSUUGHITPVAVBLWABYFHIRTXFPYDVTVFVLWB
      WAAXIWSSZXSWSSZUCZXIXSUDUEZXLXSXFUGZUDUEZUJZUJZAXNWSSZUUIUCZXNXSUDUEZXQUU
      LUDUEZUJZUJBUAUUQUUTBUUQBUKUURUUSBUURBUKBCXQUULUDBCXNXFBCRHULZCUABUMUNYFU
      DSBUKBCXSXFUVACUBBUMUNWCUPUPXTUUJUUQUUNUUTXTUUHUUPAUUIXIXNWSURWDXTUUKUURU
      UMUUSXIXNXSUDWEXTXLXQUULUDYAWFVBVBAUUHYFWSSZUCZXIYFUDUEZXLYKUDUEZUJZUJUUO
      CUBCUBUQZUVCUUJUVFUUNUVGUVBUUIAUUHYFXSWSURWGUVGUVDUUKUVEUUMYFXSXIUDWIUVGY
      KUULXLUDYFXSXFUTWHVBVBUVCUVDHIUDUEUVEKUVCXLHYKIUDAUUHYCUVBUUHAXJYCXIWJYEV
      QZWKAUVBUUFUUHAUUHUIZYCUJAUVBUIZUUFUJBCUVJUUFBUVJBUKBUAUAYKIBUAYFXFBUARHU
      LUACBUMUNXNISBUKWLUPUUGUVIUVJYCUUFUUGUUHUVBAXIYFWSURUSUUGXLYKHIXIYFXFUTPV
      MVBUVHVIWMWNWOWAVIWPXEXGFXHGUDXEXCFTSZXGFVCAXCXDWQAXCUVKXDAXCUVKUUBAXCUIZ
      UVKUJBDRXIDVCZXKUVLYBUVKUVMXJXCAXIDRURUSUVMHFTNVAVBLWRWTWKBDHFRTXFNYDVTVF
      XEXDGTSZXHGVCAXCXDXAAXDUVNXCAXDUVNUUBAXDUIZUVNUJBERXIEVCZXKUVOYBUVNUVPXJX
      DAXIERURUSUVPHGTOVAVBLWRWTWMBEHGRTXFOYDVTVFWNXB $.
      $( [25-Sep-2014] $)
  $}

  ${
    $d B a b x $.  $d C a b x $.  $d D a b x y $.  $d E a b x $.  $d F a b x $.
    $d A a b y $.  $d ph a b x y $.
    oddcomabszz.1 $e |- ( ( ph /\ x e. ZZ ) -> A e. RR ) $.
    oddcomabszz.2 $e |- ( ( ph /\ x e. ZZ /\ 0 <_ x ) -> 0 <_ A ) $.
    oddcomabszz.3 $e |- ( ( ph /\ y e. ZZ ) -> C = -u B ) $.
    oddcomabszz.4 $e |- ( x = y -> A = B ) $.
    oddcomabszz.5 $e |- ( x = -u y -> A = C ) $.
    oddcomabszz.6 $e |- ( x = D -> A = E ) $.
    oddcomabszz.7 $e |- ( x = ( abs ` D ) -> A = F ) $.
    $( An odd function which takes nonnegative values on nonnegative arguments
       commutes with ` abs ` .  (Contributed by Stefan O'Rear, 26-Sep-2014.) $)
    oddcomabszz $p |- ( ( ph /\ D e. ZZ ) -> ( abs ` E ) = F ) $=
      ( va wcel cc0 cle vb cz wa csb cabs wceq cv wi eleq1 anbi2d csbeq1 fveq2d
      cfv fveq2 csbeq1d eqeq12d imbi12d wbr wo cr 0re zre letric sylancr adantl
      ax-17 ax17el hbcsb1 hbel hbim weq csbeq1a eleq1d adantr w3a hbbr 3anbi23d
      chvar breq2 breq2d 3expa absid ad2antlr sylancom eqtr4d cneg negex csbief
      syl2anc negeq syl5eqr negeqd absnid znegcl vtoclf 3expia sylan2 sylibd wb
      vex le0neg1 syl 3imtr4d 3eqtr4rd jaodan mpdan vtoclg anabsi7 a17d csbiegf
      imp fvex a1i 3eqtr3d ) AGUBRZUCZBGDUDZUEUMZBGUEUMZDUDZHUEUMZIAXOXRXTUFZAQ
      UGZUBRZUCZBYCDUDZUEUMZBYCUEUMZDUDZUFZUHXPYBUHQGUBYCGUFZYEXPYJYBYKYDXOAYCG
      UBUIUJYKYGXRYIXTYKYFXQUEBYCGDUKULYKBYHXSDYCGUEUNUOUPUQYESYCTURZYCSTURZUSZ
      YJYDYNAYDSUTRYCUTRZYNVAYCVBZSYCVCVDVEYEYLYJYMYEYLUCZYGYFYIYQYFUTRZSYFTURZ
      YGYFUFYEYRYLABUGZUBRZUCZDUTRZUHYEYRUHBQYEYRBYEBVFBUAQYFUTBUAYCDUAQBVGVHZY
      OBVFVIVJBQVKZUUBYEUUCYRUUEUUAYDAYTYCUBUIZUJUUEDYFUTBYCDVLZVMUQJVRZVNAYDYL
      YSAUUASYTTURZVOZSDTURZUHZAYDYLVOZYSUHBQUUMYSBUUMBVFBUASYFTUAUGZSRBVFZUUNT
      RBVFZUUDVPVJUUEUUJUUMUUKYSUUEUUAYDUUIYLAUUFYTYCSTVSVQUUEDYFSTUUGVTUQKVRWA
      YFWBWIYQBYHYCDYEYLYOYHYCUFYDYOAYLYPWCYCWBWDUOWEYEYMUCZBYCWFZDUDZYFWFZYIYG
      YEUUSUUTUFZYMACUGZUBRZUCZFEWFZUFZUHYEUVAUHZCQUVGCVFCQVKZUVDYEUVFUVAUVHUVC
      YDAUVBYCUBUIUJUVHFUUSUVEUUTUVHFBUVBWFZDUDUUSBQUVIDFUVBWGYCFRBVFNWHUVHBUVI
      UURDUVBYCWJUOWKUVHEYFUVHEBUVBDUDYFBQUVBDECWTYCERBVFMWHBUVBYCDUKWKWLUPUQLV
      RZVNUUQBYHUURDYEYMYOYHUURUFYDYOAYMYPWCYCWMWDUOUUQYRYFSTURZYGUUTUFYEYRYMUU
      HVNYEYMUVKYESUURTURZSUUTTURZYMUVKYEUVLSUUSTURZUVMYDAUURUBRZUVLUVNUHYCWNAU
      VOUVLUVNUULAUVOUVLVOZUVNUHBUURUVPUVNBUVPBVFBUASUUSTUUOUUPBUAUURDUUNUURRBV
      FVHVPVJYCWGYTUURUFZUUJUVPUUKUVNUVQUUAUVOUUIUVLAYTUURUBUIYTUURSTVSVQUVQDUU
      SSTBUURDVLVTUQKWOWPWQYEUUSUUTSTUVJVTWRYEYOYMUVLWSYDYOAYPVEYCXAXBYEYRUVKUV
      MWSUUHYFXAXBXCXKYFWMWIXDXEXFXGXHXOXRYAUFAXOXQHUEBQGDHUBXOYCHRBXIOXJULVEXT
      IUFXPBQXSDIGUEXLYCIRBVFPWHXMXN $.
      $( [26-Sep-2014] $)
  $}

  ${
    $d a b c x y $.  $d a b c x A $.  $d ps a b c x $.  $d ch a b c x $.
    $d th a b c x $.  $d ta a b c x $.  $d et a b c x $.  $d rh a b c x $.
    $d ph a b c y $.
    2nn0ind.1 $e |- ps $.
    2nn0ind.2 $e |- ch $.
    2nn0ind.3 $e |- ( y e. NN -> ( ( th /\ ta ) -> et ) ) $.
    2nn0ind.4 $e |- ( x = 0 -> ( ph <-> ps ) ) $.
    2nn0ind.5 $e |- ( x = 1 -> ( ph <-> ch ) ) $.
    2nn0ind.6 $e |- ( x = ( y - 1 ) -> ( ph <-> th ) ) $.
    2nn0ind.7 $e |- ( x = y -> ( ph <-> ta ) ) $.
    2nn0ind.8 $e |- ( x = ( y + 1 ) -> ( ph <-> et ) ) $.
    2nn0ind.9 $e |- ( x = A -> ( ph <-> rh ) ) $.
    $( Induction on natural numbers with two base cases, for use with
       Lucas-type sequences.  (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
    2nn0ind $p |- ( A e. NN0 -> rh ) $=
      ( c1 va cn0 wcel wsbc caddc co cmin wa cn nn0p1nn cv wceq wb oveq1 dfsbcq
      wsb syl anbi12d weq ovex cc0 ax-1cn subidi eqeq2i sylbi sbcie mpbir elexi
      cc pm3.2i simprr nncn pncan sylancl adantr mpbird vex anbi12i 3imtr4g imp
      jca ex nnind nn0cn biimpa adantrr mpdan sbcieg mpbid ) JUBUCZAHJUDZGWJAHJ
      TUEUFZTUGUFZUDZAHWLUDZUHZWKWJWLUIUCWPJUJAHUAUKZTUGUFZUDZAHUAUPZUHAHTTUGUF
      ZUDZAHTUDZUHAHIUKZTUGUFZUDZAHIUPZUHZAHXDTUEUFZTUGUFZUDZAHXIUDZUHZWPUAIWLW
      QTULZWSXBWTXCXNWRXAULWSXBUMWQTTUGUNAHWRXAUOUQAHWQTUOURUAIUSZWSXFWTXGXOWRX
      EULWSXFUMWQXDTUGUNAHWRXEUOUQAHWQXDUOURWQXIULZWSXKWTXLXPWRXJULWSXKUMWQXITU
      GUNAHWRXJUOUQAHWQXIUOURWQWLULZWSWNWTWOXQWRWMULWSWNUMWQWLTUGUNAHWRWMUOUQAH
      WQWLUOURXBXCXBBKABHXATTUGUTHUKZXAULXRVAULABUMXAVAXRTVBVCVDNVEVFVGXCCLACHT
      TVIVBVHOVFVGVJXDUIUCZXHXMXSXHUHZXKXLXTXKXGXSXFXGVKXTXJXDULZXKXGUMXSYAXHXS
      XDVIUCTVIUCZYAXDVLVBXDTVMVNVOAHXJXDUOUQVPXSXHXLXSDEUHFXHXLMXFDXGEADHXEXDT
      UGUTPVFAEHXDIVQQVFVRAFHXIXDTUEUTRVFVSVTWAWBWCUQWJWNWKWOWJWNWKWJWMJULZWNWK
      UMWJJVIUCYBYCJWDVBJTVMVNAHWMJUOUQWEWFWGAGHJUBSWHWI $.
      $( [1-Oct-2014] $)
  $}

  ${
    $d ph a b y $.  $d A a b x y $.  $d ps a b x $.  $d ch a b x $.
    $d th a b x $.  $d ta a b x $.
    zindbi.1 $e |- ( y e. ZZ -> ( ps <-> ch ) ) $.
    zindbi.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    zindbi.3 $e |- ( x = ( y + 1 ) -> ( ph <-> ch ) ) $.
    zindbi.4 $e |- ( x = 0 -> ( ph <-> th ) ) $.
    zindbi.5 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Inductively transfer a property to the integers if it holds for zero and
       passes between adjacent integers in either direction.  (Contributed by
       Stefan O'Rear, 1-Oct-2014.) $)
    zindbi $p |- ( A e. ZZ -> ( th <-> ta ) ) $=
      ( vb cz wcel cc0 cle wb dfsbcq va wbr wo cr 0re zre letric sylancr 0z w3a
      wsbc wi cv wceq eleq1 breq1 3anbi13d bibi1d imbi12d breq2 3anbi23d bibi2d
      wsb c1 caddc co weq biidd vex sbcie syl5bbr ovex bibi12d vtoclga 3ad2ant2
      oveq1 biimpd uzind vtocl2g 3adant3 pm2.43i mp3an1 wa mp3an2 bicomd jaodan
      syl mpdan elexi a1i sbcieg 3bitr3d ) HOPZAFQUKZAFHUKZDEWMQHRUBZHQRUBZUCZW
      NWOSZWMQUDPHUDPWRUEHUFQHUGUHWMWPWSWQQOPZWMWPWSUIWTWMWPUJZWSWTWMXAWSULZWPG
      UMZOPZNUMZOPZXCXERUBZUJZAFGVCZAFNVCZSZULZWTXFQXERUBZUJZWNXJSZULXBGNQHOOXC
      QUNZXHXNXKXOXPXDWTXGXMXFXCQOUOXCQXERUPUQXPXIWNXJAFXCQTURUSXEHUNZXNXAXOWSX
      QXFWMXMWPWTXEHOUOXEHQRUTVAXQXJWOWNAFXEHTVBUSXIAFUAVCZSXIXISXKXIAFXEVDVEVF
      ZUKZSZXKUANXCXEUAGVGXRXIXIAFUAUMZXCTVBUANVGXRXJXIAFYBXETVBZYBXSUNXRXTXIAF
      YBXSTVBYCXDXIVHXHXKYAXHXJXTXIXFXDXJXTSZXGBCSYDGXEOGNVGZBXJCXTBXIYEXJABFXC
      GVIJVJAFXCXETVKCAFXCVDVEVFZUKZYEXTACFYFXCVDVEVLKVJYEYFXSUNYGXTSXCXEVDVEVP
      AFYFXSTWGVKVMIVNVOVBVQVRZVSVTWAWBWMWQWCWOWNWMWTWQWOWNSZUIWMWTWQUJZYIWMWTY
      JYIULZWQXLWMXFHXERUBZUJZWOXJSZULYKGNHQOOXCHUNZXHYMXKYNYOXDWMXGYLXFXCHOUOX
      CHXERUPUQYOXIWOXJAFXCHTURUSXEQUNZYMYJYNYIYPXFWTYLWQWMXEQOUOXEQHRUTVAYPXJW
      NWOAFXEQTVBUSYHVSVTWAWDWEWFWHWNDSWMADFQQOUIWILVJWJAEFHOMWKWL $.
      $( [1-Oct-2014] $)
  $}

  ${
    $d A a b $.  $d B a b $.  $d N a b $.

    $( Mantissa ordering relationship for exponentiation.  (Contributed by
       Stefan O'Rear, 16-Oct-2014.) $)
    expmordi $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ A < B ) /\
        N e. NN ) -> ( A ^ N ) < ( B ^ N ) ) $=
      ( va vb cr wcel wa cc0 wbr clt cexp co wi c1 oveq2 breq12d imbi2d syl2anc
      wceq cle cn cv caddc weq cc wb recn exp1 breqan12d syl2an biimpar adantrl
      w3a cn0 simp2ll nnnn0 3ad2ant1 reexpcl simp2lr jca simp2rl expge0 syl3anc
      cmul simp3 simp2l simp2r ltmul12a syl22anc recnd expp1 3brtr4d 3exp nnind
      a2d impcom 3impa ) AFGZBFGZHZIAUAJZABKJZHZCUBGZACLMZBCLMZKJZWEWAWDHZWHWIA
      DUCZLMZBWJLMZKJZNWIAOLMZBOLMZKJZNWIAEUCZLMZBWQLMZKJZNWIAWQOUDMZLMZBXALMZK
      JZNWIWHNDECWJOTZWMWPWIXEWKWNWLWOKWJOALPWJOBLPQRDEUEZWMWTWIXFWKWRWLWSKWJWQ
      ALPWJWQBLPQRWJXATZWMXDWIXGWKXBWLXCKWJXAALPWJXABLPQRWJCTZWMWHWIXHWKWFWLWGK
      WJCALPWJCBLPQRWAWCWPWBWAWPWCVSAUFGZBUFGZWPWCUGVTAUHBUHXIXJWNAWOBKAUIBUIUJ
      UKULUMWQUBGZWIWTXDXKWIWTXDXKWIWTUNZWRAVEMZWSBVEMZXBXCKXLWRFGZWSFGZHIWRUAJ
      ZWTHWAWDXMXNKJXLXOXPXLVSWQUOGZXOVSVTWDXKWTUPZXKWIXRWTWQUQURZAWQUSSXLVTXRX
      PVSVTWDXKWTUTZXTBWQUSSVAXLXQWTXLVSXRWBXQXSXTWBWCWAXKWTVBAWQVCVDXKWIWTVFVA
      XKWAWDWTVGXKWAWDWTVHWRWSABVIVJXLXIXRXBXMTXLAXSVKXTAWQVLSXLXJXRXCXNTXLBYAV
      KXTBWQVLSVMVNVPVOVQVR $.
      $( [16-Oct-2014] $)

    $( Mantissa ordering relationship for exponentiation of positive reals.
       (Contributed by Stefan O'Rear, 16-Oct-2014.) $)
    rpexpmord $p |- ( ( N e. NN /\ A e. RR+ /\ B e. RR+ ) -> ( A < B <->
        ( A ^ N ) < ( B ^ N ) ) ) $=
      ( va vb cn wcel crp clt wbr cexp co wb cv oveq1 rpssre cr rpre wa syl cn0
      nnnn0 reexpcl syl2anr cc0 cle simplrl simplrr rpge0 simpr simpll expmordi
      syl221anc ex ltord1 3impb ) CFGZAHGBHGABIJACKLZBCKLZIJMUQDEDNZCKLZENZCKLZ
      ABHURUSUTVBCKOUTACKOUTBCKOPUTHGZUTQGZCUAGVAQGUQUTRZCUBUTCUCUDUQVDVBHGZSZS
      ZUTVBIJZVAVCIJZVIVJSZVEVBQGZUEUTUFJZVJUQVKVLVDVEUQVDVGVJUGZVFTVLVGVMUQVDV
      GVJUHVBRTVLVDVNVOUTUITVIVJUJUQVHVJUKUTVBCULUMUNUOUP $.
      $( [16-Oct-2014] $)
  $}

