$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Uncategorized stuff not associated with a major project
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y A $.  $d x y B $.
    $( Epsilon induction for sets contained in a transitive set.  If we are
       allowed to assume Infinity, then all sets have a transitive closure and
       this reduces to ~ setind ; however, this version is useful without
       Infinity.  (Contributed by Stefan O'Rear, 28-Oct-2014.) $)
    setindtr $p |- ( A. x ( x C_ A -> x e. A ) -> ( E. y ( Tr y /\ B e. y ) ->
        B e. A ) ) $=
      ( cv wtr wcel wa wex wss wi wal cdif c0 wceq cin wn sylib adantlr ex wrex
      wral ax-17 hba1 hban eldifn adantl wel eldifi trss syl5 df-ss sseq1d ax-4
      imp ad2antlr sylbid mtod inssdif0 sylnib ralrimi ralnex difss ssexi zfreg
      vex necon1bi syl ssdif0 sylibr simplr sseldd exlimiv com12 ) BEZFZDVOGZHZ
      BIAEZCJZVSCGZKZALZDCGZVRWCWDKBVRWCWDVRWCHVOCDVPWCVOCJZVQVPWCHZVOCMZNOZWEW
      FVSWGPNOZAWGUAZQZWHWFWIQZAWGUBWKWFWLAWGVPWCAVPAUCWBAUDUEWFVSWGGZWLWFWMHZV
      SVOPZCJZWIWNWPWAWMWAQWFVSVOCUFUGWNWPVTWAWNWOVSCVPWMWOVSOZWCVPWMHVSVOJZWQV
      PWMWRWMABUHVPWRVSVOCUIVOVSUJUKUOVSVOULRSUMWCWBVPWMWBAUNUPUQURVSVOCUSUTTVA
      WIAWGVBRWJWGNAWGWGVOBVFVOCVCVDVEVGVHVOCVIVJSVPVQWCVKVLTVMVN $.
      $( [28-Oct-2014] $)
  $}

  ${
    $d B a x z $.  $d ph y $.  $d ps x $.  $d ch x $.  $d ph a z $.
    $d a x y $.
    setindtrs.a $e |- ( A. y e. x ps -> ph ) $.
    setindtrs.b $e |- ( x = y -> ( ph <-> ps ) ) $.
    setindtrs.c $e |- ( x = B -> ( ph <-> ch ) ) $.
    $( Epsilon induction scheme without Infinity.  See comments at
       ~ setindtr .  (Contributed by Stefan O'Rear, 28-Oct-2014.) $)
    setindtrs $p |- ( E. z ( Tr z /\ B e. z ) -> ch ) $=
      ( va cv wtr wcel wa wex wi wral hbab1 cvv cab setindtr dfss3 ax17el hbral
      wss hbim weq raleq eleq1 imbi12d vex elab ralbii abid 3imtr4i chvar sylbi
      mpg wb elex adantl exlimiv elabg syl mpbid ) FLZMZGVGNZOZFPZGADUAZNZCKLZV
      LUFZVNVLNZQVKVMQKKFVLGUBVOELZVLNZEVNRZVPEVNVLUCVREDLZRZVTVLNZQVSVPQDKVSVP
      DVRDEVNEKDUDADESUEADKSUGDKUHWAVSWBVPVREVTVNUIVTVNVLUJUKBEVTRAWAWBHVRBEVTA
      BDVQEULIUMUNADUOUPUQURUSVKGTNZVMCUTVJWCFVIWCVHGVGVAVBVCACDGTJVDVEVF $.
      $( [28-Oct-2014] $)
  $}

  ${
    $d a b c x y $.  $d N a b c x y $.

    $( Lemma for ~ dford3 . $)
    dford3lem1 $p |- ( ( Tr N /\ A. y e. N Tr y ) ->
        A. b e. N ( Tr b /\ A. y e. b Tr y ) ) $=
      ( wtr cv wral wa treq cbvralv biimpi adantl wcel wi wss trss ssralv com23
      syl6 imp ralrimiv r19.26 sylanbrc ) BDZAEZDZABFZGZCEZDZCBFZUEAUHFZCBFUIUK
      GCBFUFUJUCUFUJUEUIACBUDUHHIJKUGUKCBUCUFUHBLZUKMUCULUFUKUCULUHBNUFUKMBUHOU
      EAUHBPRQSTUIUKCBUAUB $.
      $( [28-Oct-2014] $)

    $( Lemma for ~ dford3 . $)
    dford3lem2 $p |- ( ( Tr x /\ A. y e. x Tr y ) -> x e. On ) $=
      ( vc va vb cv wtr wa wral con0 wcel vex treq anbi12d wi word sylibr raleq
      weq eleq1 wel wex csuc trsuc2 sucid sucex eleq2 cla4ev sylancl adantr wss
      wceq simprl dford3lem1 ralim syl5 imp dfss3 ordon trssord syl3anc elon ex
      a1i imbi12d setindtrs mpcom ) CFZGZACUAZHZCUBZAFZGZBFGZBVMIZHZVMJKZVNVLVP
      VNVMUCZGZVMVSKZVLVMUDVMALZUEVKVTWAHCVSVMWBUFVHVSULVIVTVJWAVHVSMVHVSVMUGNU
      HUIUJDFZGZVOBWCIZHZWCJKZOEFZGZVOBWHIZHZWHJKZOZVQVRODECVMWMEWCIZWFWGWNWFHZ
      WCPZWGWOWDWCJUKZJPZWPWNWDWEUMWOWLEWCIZWQWNWFWSWFWKEWCIWNWSBWCEUNWKWLEWCUO
      UPUQEWCJURQWRWOUSVDWCJUTVAWCDLVBQVCDESZWFWKWGWLWTWDWIWEWJWCWHMVOBWCWHRNWC
      WHJTVEDASZWFVQWGVRXAWDVNWEVPWCVMMVOBWCVMRNWCVMJTVEVFVG $.
      $( [28-Oct-2014] $)

    $( Ordinals are precisely the hereditarily transitive classes.
       (Contributed by Stefan O'Rear, 28-Oct-2014.) $)
    dford3 $p |- ( Ord N <-> ( Tr N /\ A. x e. N Tr x ) ) $=
      ( va word wtr cv wral wa ordtr wcel ordelord syl ralrimiva jca con0 simpl
      wss dford3lem1 dford3lem2 ralimi dfss3 biimpri 3syl ordon trssord syl3anc
      a1i impbii ) BDZBEZAFZEZABGZHZUIUJUMBIUIULABUIUKBJHUKDULBUKKUKILMNUNUJBOQ
      ZODZUIUJUMPUNCFZEULAUQGHZCBGUQOJZCBGZUOABCRURUSCBCASTUOUTCBOUAUBUCUPUNUDU
      GBOUEUFUH $.
      $( [28-Oct-2014] $)

    $( ~ dford3 expressed in primitives to demonstrate shortness.  (Contributed
       by Stefan O'Rear, 28-Oct-2014.) $)
    dford4 $p |- ( Ord N <-> A. a A. b A. c ( ( a e. N /\ b e. a ) ->
        ( b e. N /\ ( c e. b -> c e. a ) ) ) ) $=
      ( wtr cv wa wel wi wal dftr2 ax-17 ancom imbi1i bitri 2albii alcom bitr4i
      wcel impexp word wral dford3 df-ral imbi2i 19.21-2 anbi2i 3bitr3i 3bitr2i
      19.3 anass albii anbi12i 19.26 19.26-2 pm4.76 bitr3i 3bitri ) AUAAEZBFZEZ
      BAUBZGZUTASZCBHZGZCFASZIZDJZCJZVFDCHZDBHZIZIZDJCJZGZBJZVFVGVMGIZDJCJZBJBA
      UCVCVJBJZVOBJZGVQUSVTVBWAUSVEVDGZVGIZBJCJZVTCBAKVTWCCJBJWDVIWCBCVIVHWCVHD
      VHDLUJVFWBVGVDVEMNOPWCBCQORVBVDVAIZBJWAVABAUDWEVOBWEVDVKVEGZVLIZCJDJZIVDW
      GIZCJDJZVOVAWHVDDCUTKUEVDWGDCVDDLVDCLUFWJVNCJDJVOWIVNDCVDWFGZVLIVFVKGZVLI
      WIVNWKWLVLWKVDVEVKGZGWLWFWMVDVKVEMUGVDVEVKUKRNVDWFVLTVFVKVLTUHPVNDCQOUIUL
      OUMVJVOBUNRVPVSBVPVHVNGZDJCJVSVHVNCDUOWNVRCDVFVGVMUPPUQULUR $.
      $( [28-Oct-2014] $)
  $}

  ${
    $d ph y $.  $d x y $.  $d A y a $.
    $( Two ways to express "exactly one".  (Contributed by Stefan O'Rear,
       28-Oct-2014.) $)
    reuen1 $p |- ( E! x e. A ph <-> { x e. A | ph } ~~ 1o ) $=
      ( vy wreu crab cv csn wceq wex c1o cen wbr reusn en1 bitr4i ) ABCEABCFZDG
      HIDJQKLMABDCNDQOP $.
      $( [28-Oct-2014] $)

    $( Two ways to express "exactly one".  (Contributed by Stefan O'Rear,
       28-Oct-2014.) $)
    euen1 $p |- ( E! x ph <-> { x | ph } ~~ 1o ) $=
      ( cvv wreu crab c1o cen wbr weu cab reuen1 reuv rabab breq1i 3bitr3i ) AB
      CDABCEZFGHABIABJZFGHABCKABLPQFGABMNO $.
      $( [28-Oct-2014] $)

    $( A set has less than one member iff it is empty.  (Contributed by Stefan
       O'Rear, 28-Oct-2014.) $)
    sdom1 $p |- ( A ~< 1o <-> A = (/) ) $=
      ( va c1o csdm wbr c0 wceq wcel relsdom brrelexi cv wi breq1 eqeq1 imbi12d
      cvv wn cdom domnsym 0sdom con2i 0sdom1dom sylnibr necon2bbii sylibr mpcom
      vex vtoclg wne 1n0 con0 1on elexi mpbir mpbiri impbii ) ACDEZAFGZAPHUQURA
      CDIJBKZCDEZUSFGZLUQURLBAPUSAGUTUQVAURUSACDMUSAFNOUTFUSDEZQVAUTCUSREZVBVCU
      TCUSSUAUSBUGZUBUCVBUSFUSVDTUDUEUHUFURUQFCDEZVECFUIUJCCUKULUMTUNAFCDMUOUP
      $.
      $( [28-Oct-2014] $)

    $( Two ways to express "at most one".  (Contributed by Stefan O'Rear,
       28-Oct-2014.) $)
    modom $p |- ( E* x ph <-> { x | ph } ~<_ 1o ) $=
      ( wmo wex weu wi wn cab c1o cdom wbr df-mo imor csdm cen wceq abn0 bitr4i
      wo c0 necon1bbii sdom1 euen1 orbi12i brdom2 3bitri ) ABCABDZABEZFUGGZUHSZ
      ABHZIJKZABLUGUHMUJUKINKZUKIOKZSULUIUMUHUNUIUKTPUMUGUKTABQUAUKUBRABUCUDUKI
      UERUF $.
      $( [28-Oct-2014] $)
  $}

  $( Unrelated:  Wiener pairs treat proper classes symmetrically.  (Contributed
     by Stefan O'Rear, 19-Sep-2014.) $)
  wopprc $p |- ( ( A e. _V /\ B e. _V ) <-> -. 1o e. { { { A } , (/) } , { { B
      } } } ) $=
    ( cvv wcel wa c0 csn cpr c1o wceq wn wo pm4.56 dfsn2 snex 0ex snprc con2bii
    impbii xchbinxr id syl5reqr preqr1 syl sylibr biimpi preq1d syl6reqr bitr2i
    eqcom sneqr sneq anbi12i elpr notbii 3bitr4i df1o2 eleq1i ) ACDZBCDZEZFGZAG
    ZFHZBGZGZHZDZIVGDVBVDJZKZVBVFJZKZEVIVKLZKVAVHKVIVKMUSVJUTVLVIUSVIUSKZVIVCFJ
    ZVNVIVDFFHZJVOVIVPVBVDFNZVIUAUBVCFFAOPUCUDAQZUEVNVDVPVBVNVCFFVNVOVRUFUGVQUH
    SRUTFVEJZVKVSUTUTKVEFJVSBQVEFUJUIRVKVSFVEPUKFVEULSTUMVHVMVBVDVFFOUNUOUPIVBV
    GUQURT $.
    $( [19-Sep-2014] $)

  ${
    $d a b c d e f g h i j k l m $.  $d A a b c $.  $d B a b c $.
    $d C a b c $.

    $( Lemma for ~ ttac .  Use non-surjection to prove non-emptiness. $)
    ttaclem4 $p |- ( ( ( ( b i^i a ) = (/) /\ e e. ( b u. a ) ) /\
        -. b ~<_ a /\ d : ( ( b u. a ) X. ( b u. a ) ) -1-1-onto->
        ( b u. a ) ) -> ( b i^i ( d " ( b X. { e } ) ) ) =/= (/) ) $=
      ( vf vg vh cv cin c0 wceq wcel wa wn wi vex wel ad2antlr opelxp sylanbrc
      cun cdom wbr cxp wf1o w3a csn cima wne simp2 cvv cop wo wfun f1ofun elun1
      cfv cdm adantl simpllr f1odm eleqtrrd simpr snid a1i funfvima imp adantlr
      syl21anc incom eqeq1i wral disj eleq1 notbid rcla4cv sylbi wf f1of simplr
      mpd syl ad2antrr ffvelrn syl2anc elun sylib orel1 sylc ex weq wb ad2antrl
      wf1 f1of1 ad2antll jca f1fveq opth1 syl6bi opeq1 fveq2d impbid1 dom2d mpi
      adantr necon3bd 3adant2 ) CHZBHZIJKZAHZXIXJUAZLZMZXIXJUBUCZNZXMXMUDZXMDHZ
      UEZUFXQXIXSXIXLUGZUDZUHZIZJUIZXOXQXTUJXOXTXQYEOXQXOXTMZXPYDJYFYDJKZXPYFYG
      MZXIUKLXPCPYHEFXIXJEHZXLULZXSUQZFHZXLULZXSUQZUKYHECQZYKXJLZYHYOMZYKXILZNZ
      YRYPUMZYPYQYKYCLZYSYFYOUUAYGYFYOMZXSUNZYJXSURZLZYJYBLZUUAXTUUCXOYOXRXMXSU
      ORUUBYJXRUUDUUBYIXMLZXNYJXRLZYOUUGYFYIXIXJUPZUSXKXNXTYOUTYIXLXMXMAPZSZTXT
      UUDXRKXOYOXRXMXSVARVBUUBYOXLYALZUUFYFYOVCUULUUBXLUUJVDVEYIXLXIYAUUJSTUUCU
      UEMUUFUUAYBYJXSVFVGVIVHYGUUAYSOZYFYOYGYCXIIZJKZUUMYDUUNJXIYCVJVKUUOGCQZNZ
      GYCVLUUMGYCXIVMUUQYSGYKYCGHZYKKUUPYRUURYKXIVNVOVPVQVQRWAYQYKXMLZYTYQXRXMX
      SVRZUUHUUSYQXTUUTXOXTYGYOUTXRXMXSVSWBYQUUGXNUUHYOUUGYHUUIUSYFXNYGYOXKXNXT
      VTWCUUKTXRXMYJXSWDWEYKXIXJWFWGYRYPWHWIWJYFYOFCQZMZYKYNKZEFWKZWLZOYGYFUVBU
      VEYFUVBMZUVCUVDUVFUVCYJYMKZUVDUVFXRXMXSWNZUUHYMXRLZMZUVCUVGWLXTUVHXOUVBXR
      XMXSWORXOUVBUVJXTXOUVBMZUUHUVIUVKUUGXNUUHYOUUGXOUVAUUIWMXKXNUVBVTZUUKTUVK
      YLXMLZXNUVIUVAUVMXOYOYLXIXJUPWPUVLYLXLXMXMUUJSTWQVHXRXMYJYMXSWRWEYIXLYLXL
      EPWSWTUVDYJYMXSYIYLXLXAXBXCWJXFXDXEWJXGXHWA $.
      $( [5-Nov-2014] $)

    $( Lemma for ~ ttac . $)
    ttaclem5 $p |- ( ( ( ( b i^i a ) = (/) /\ -. b ~<_ a /\ d : ( ( b u. a ) X.
        ( b u. a ) ) -1-1-onto-> ( b u. a ) ) /\ b e. On ) -> a ~<_ b ) $=
      ( ve vf vg cv cin c0 wceq cxp con0 wcel wa cima wss wne adantl simpll3 ex
      cdom wbr wn cun wf1o w3a cvv vex csn cint wel inss1 onss ad2antlr simpll1
      syl5ss elun2 simpll2 ttaclem4 syl211anc syl2anc sseldi weq wb wex adantrr
      onint adantr wi adantrl eleq1a syl imp elin sylanbrc eleq1 cla4egv inindi
      sylc n0 ccnv wfun wf1 f1of1 wf df-f1 simprbi eqcomd ineq2d xpindi imaeq2i
      imain ineq2i disjsn2 xpeq2d imaeq2d xp0 in0 3eqtri syl6eq syl5eqr necon1d
      ima0 eqtrd syl5bir sylsyld sneq inteqd impbid1 dom2d mpi ) BGZAGZHIJZXLXM
      UAUBUCZXLXMUDZXPKZXPCGZUEZUFZXLLMZNZXMUGMXMXLUAUBAUHYBDEXMXLXLXRXLDGZUIZK
      ZOZHZUJZXLXRXLEGZUIZKZOZHZUJZUGYBDAUKZYHXLMYBYONZYGXLYHXLYFULZYPYGLPYGIQZ
      YHYGMZYPYGXLLYQYAXLLPZXTYOXLUMZUNUPYPXNYCXPMZXOXSYRXNXOXSYAYOUOYOUUBYBYCX
      MXLUQRXNXOXSYAYOURXNXOXSYAYOSDABCUSUTYGVGVAZVBTYBYOEAUKZNZYHYNJZDEVCZVDYB
      UUENZUUFUUGUUHXSUUFFGZYGYMHZMZFVEZUUGXNXOXSYAUUESUUHUUFUULUUHUUFNZYSYHUUJ
      MZUULUUHYSUUFYBYOYSUUDUUCVFVHZUUMYSYHYMMZUUNUUOUUHUUFUUPUUHYNYMMZUUFUUPVI
      YBUUDUUQYOYBUUDNZYMLPYMIQZUUQUURYMXLLXLYLULYAYTXTUUDUUAUNUPUURXNYIXPMZXOX
      SUUSXNXOXSYAUUDUOUUDUUTYBYIXMXLUQRXNXOXSYAUUDURXNXOXSYAUUDSEABCUSUTYMVGVA
      VJYNYMYHVKVLVMYHYGYMVNVOUUKUUNFYHYGUUIYHUUJVPVQVSTUULUUJIQXSUUGFUUJVTXSYC
      YIUUJIXSYCYIQZUUJIJXSUVANZUUJXLYFYLHZHZIXLYFYLVRUVBUVDXLXRYEYKHZOZHZIUVBU
      VCUVFXLUVBUVFUVCUVBXRWAWBZUVFUVCJXSUVHUVAXSXQXPXRWCZUVHXQXPXRWDUVIXQXPXRW
      EUVHXQXPXRWFWGVLVHYEYKXRWLVLWHWIUVBUVGXLXRXLYDYJHZKZOZHZIUVLUVFXLUVKUVEXR
      XLYDYJWJWKWMUVAUVMIJXSUVAUVMXLXRXLIKZOZHZIUVAUVLUVOXLUVAUVKUVNXRUVAUVJIXL
      YCYIWNWOWPWIUVPXLXRIOZHXLIHIUVOUVQXLUVNIXRXLWQWKWMUVQIXLXRXCWMXLWRWSWTRXA
      XDXATXBXEXFUUGYGYMUUGYFYLXLUUGYEYKXRUUGYDYJXLYCYIXGWOWPWIXHXITXJXK $.
      $( [5-Nov-2014] $)

    $( Lemma for ~ ttac . $)
    ttaclem6 $p |- ( ( ( ( b i^i a ) = (/) /\ -. b ~<_ a /\ -. a e. Fin ) /\
        ( b e. On /\ A. c ( om ~<_ c -> ( c X. c ) ~~ c ) ) ) -> a ~<_ b ) $=
      ( vd cv wceq cdom wbr wn wcel com cxp cen wi wa cvv wss vex wex breq2 cin
      cfn w3a con0 wal cun unex simpl2 wral isinf weq anbi2d exbidv rcla4cv syl
      3ad2ant3 ensym ssdomg ax-mp endomtr syl2anr exlimiv syl6 adantr mtod word
      c0 wb ordom eloni adantl ordtri1 sylancr mpbird ssun1 syl6ss mpsyl xpeq12
      ssdom2g anidms id breq12d imbi12d cla4v syl5com wf1o bren simpll1 simpll2
      simpr simplr ttaclem5 syl31anc ex exlimdv syl5bi syld impr ) BEZAEZUAVGFZ
      WSWTGHZIZWTUBJIZUCZWSUDJZKCEZGHZXGXGLZXGMHZNZCUEZWTWSGHZXEXFOZXLWSWTUFZXO
      LZXOMHZXMXNKXOGHZXLXQXOPJXNKXOQXRWSWTBRZARUGZXNKWSXOXNKWSQZWSKJZIZXNYBXBX
      AXCXDXFUHXEYBXBNXFXEYBDEZWTQZYDWSMHZOZDSZXBXDXAYBYHNZXCXDYEYDXGMHZOZDSZCK
      UIYIDWTCUJYLYHCWSKCBUKZYKYGDYMYJYFYEXGWSYDMTULUMUNUOUPYGXBDYFWSYDMHYDWTGH
      ZXBYEYDWSXSUQYDPJYEYNNDRYDWTPURUSWSYDWTUTVAVBVCVDVEXNKVFWSVFZYAYCVHVIXFYO
      XEWSVJVKKWSVLVMVNWSWTVOVPKXOPVSVQXKXRXQNCXOXTXGXOFZXHXRXJXQXGXOKGTYPXIXPX
      GXOMYPXIXPFXGXOXGXOVRVTYPWAWBWCWDWEXQXPXOXGWFZCSXNXMXPXOCXTWGXNYQXMCXNYQX
      MXNYQOXAXCYQXFXMXAXCXDXFYQWHXAXCXDXFYQWIXNYQWJXEXFYQWKABCWLWMWNWOWPWQWR
      $.
      $( [5-Nov-2014] $)

    $( Lemma for ~ ttac . $)
    ttaclem7 $p |- ( ( ( On i^i a ) = (/) /\ A. c ( om ~<_ c ->
        ( c X. c ) ~~ c ) ) -> E. b e. On b ~~ a ) $=
      ( vd cv wcel con0 cin c0 wceq cdom wbr cen wi wa wrex cvv breq1 syl2anc
      wn cfn com cxp wal finnum a1d crab vex hartogs ax-mp elirr cbvrabv elrab2
      biimpri mpan mto notbid rcla4ev mp2an simprl onss ad2antrl simplrl ssdisj
      wss simprr simpll simplrr ttaclem6 syl32anc ra4e ondomen syl rexlimdva ex
      expr mpi pm2.61i ) AEZUAFZGVSHIJZUBCEZKLWBWBUCWBMLNCUDZOZBEZVSMLBGPZNVTWF
      WDBVSUEUFVTTZWDWFWGWDOZDEZVSKLZTZDGPZWFWEVSKLZBGUGZGFZWNVSKLZTZWLVSQFWOAU
      HBVSQUIUJZWPWNWNFZWNUKWOWPWSWRWSWOWPOWBVSKLZWPCWNGWNWBWNVSKRWMWTBCGWEWBVS
      KRULUMUNUOUPWKWQDWNGWIWNJWJWPWIWNVSKRUQURUSWHWKWFDGWHWIGFZWKWFWHXAWKOZOZV
      SWIKLZDGPZWFXCXAXDXEWHXAWKUTZXCWIVSHIJZWKWGXAWCXDXCWIGVEZWAXGXAXHWHWKWIVA
      VBWGWAWCXBVCWIGVSVDSWHXAWKVFWGWDXBVGXFWGWAWCXBVHADCVIVJXDDGVKSDBVSVLVMVPV
      NVQVOVR $.
      $( [5-Nov-2014] $)

    $( Lemma for ~ ttac . $)
    ttaclem8 $p |- ( A. c ( om ~<_ c -> ( c X. c ) ~~ c ) ->
        E. b e. On b ~~ a ) $=
      ( vd com cv wbr cxp cen wi c1o con0 wrex cin c0 wceq incom cvv eqtri wa
      cdom wal wss xpss onxpdisj ssdisj mp2an vex 1onn elexi xpex eqeq1d anbi1d
      ineq2 breq2 rexbidv imbi12d ttaclem7 vtocl mpan xp1en entr mpan2 reximi
      syl ) ECFZUAGVFVFHVFIGJCUBZBFZAFZKHZIGZBLMZVHVIIGZBLMLVJNZOPZVGVLVNVJLNZO
      LVJQVJRRHZUCVQLNZOPVPOPVIKUDVRLVQNOVQLQUESVJVQLUFUGSLDFZNZOPZVGTZVHVSIGZB
      LMZJVOVGTZVLJDVJVIKAUHZKEUIUJUKVSVJPZWBWEWDVLWGWAVOVGWGVTVNOVSVJLUNULUMWG
      WCVKBLVSVJVHIUOUPUQDBCURUSUTVKVMBLVKVJVIIGVMVIWFVAVHVJVIVBVCVDVE $.
      $( [5-Nov-2014] $)

    $( Tarski's theorem about choice: ~ infxpidm is equivalent to ~ ax-ac .
       (Contributed by Stefan O'Rear, 4-Nov-2014.) $)
    ttac $p |- ( A. a E. b b We a <-> A. c ( om ~<_ c -> ( c X. c ) ~~ c ) ) $=
      ( vd cv wwe wex wal cen wbr con0 wrex com cdom cxp cvv wcel vex alrimiv
      wi wb ween ax-mp albii cfn wn weq breq2 rexbidv domnsym isfinite3 sylnibr
      a4v csdm infxpidm2 ex syl2im ttaclem8 impbii bitri ) AEZBEFBGZAHDEZVAIJZD
      KLZAHZMCEZNJZVGVGOVGIJZTZCHZVBVEAVAPQVBVEUAARDVAPBUBUCUDVFVKVFVJCVFVCVGIJ
      ZDKLZVHVGUEQZUFZVIVEVMACACUGVDVLDKVAVGVCIUHUIUMVHVGMUNJVNMVGUJVGUKULVMVOV
      IDVGCRUOUPUQSVKVEAADCURSUSUT $.
      $( [4-Nov-2014] $)
  $}

