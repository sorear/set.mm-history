$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Congruential equations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A wff of the form ` A || ( B - C ) ` is interpreted as a congruential
     equation.  This is similar to ` ( B mod A ) = ( C mod A ) ` , but is
     defined such that behavior is regular for zero and negative values of
     ` A ` .  To use this concept effectively, we need to show that
     congruential equations behave similarly to normal equations; first a
     transitivity law.  Idea for the future:  If there was a congruential
     equation symbol, it could incorporate type constraints, so that most of
     these would not need them.  (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
  congtr $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. ZZ /\ D e. ZZ ) /\ ( A || (
      B - C ) /\ A || ( C - D ) ) ) -> A || ( B - D ) ) $=
    ( cz wcel wa co cdivides wbr w3a caddc simp1l simp1r zsubcl 3ad2ant2 cc zcn
    cmin adantl simp2l syl2anc simp3 dvds2add imp syl31anc wceq 3ad2ant1 adantr
    npncan syl3anc breqtrd ) AEFZBEFZGZCEFZDEFZGZABCSHZIJACDSHZIJGZKZAUSUTLHZBD
    SHZIVBUMUSEFZUTEFZVAAVCIJZUMUNURVAMVBUNUPVEUMUNURVANUOUPUQVAUABCOUBURUOVFVA
    CDOPUOURVAUCUMVEVFKVAVGAUSUTUDUEUFVBBQFZCQFZDQFZVCVDUGUOURVHVAUNVHUMBRTUHUR
    UOVIVAUPVIUQCRUIPURUOVJVAUQVJUPDRTPBCDUJUKUL $.
    $( [1-Oct-2014] $)

  $( If two pairs of numbers are componentwise congruent, so are their sums.
     (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
  congadd $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) /\ ( D e. ZZ /\ E e. ZZ )
      /\ ( A || ( B - C ) /\ A || ( D - E ) ) ) -> A || ( ( B + D ) - ( C + E )
      ) ) $=
    ( cz wcel w3a wa cmin co cdivides wbr caddc wi simpl1 zsubcl cc zcn syl
    3adant1 adantr adantl dvds2add syl3anc 3impia wceq simpl2 ad2antrl ad2antll
    simpl3 addsub4 syl22anc 3adant3 breqtrrd ) AFGZBFGZCFGZHZDFGZEFGZIZABCJKZLM
    ADEJKZLMIZHAVCVDNKZBDNKCENKJKZLUSVBVEAVFLMZUSVBIZUPVCFGZVDFGZVEVHOUPUQURVBP
    USVJVBUQURVJUPBCQUAUBVBVKUSDEQUCAVCVDUDUEUFUSVBVGVFUGZVEVIBRGZDRGZCRGZERGZV
    LVIUQVMUPUQURVBUHBSTUTVNUSVADSUIVIURVOUPUQURVBUKCSTVAVPUSUTESUJBDCEULUMUNUO
    $.
    $( [1-Oct-2014] $)

  $( If two pairs of numbers are componentwise congruent, so are their
     products.  (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
  congmul $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) /\ ( D e. ZZ /\ E e. ZZ )
      /\ ( A || ( B - C ) /\ A || ( D - E ) ) ) -> A || ( ( B x. D ) - ( C x. E
      ) ) ) $=
    ( cz wcel w3a wa cmin cdivides wbr cmul zmulcl syl2anc 3ad2ant2 syl3anc zcn
    co cc simp11 simp12 simp2l simp2r simp13 simp3r wi dvdsmultr2 wceq 3ad2ant1
    zsubcl adantr adantl subdi breqtrd simp3l dvdsmultr1 3ad2ant3 subdir congtr
    mpd syl222anc ) AFGZBFGZCFGZHZDFGZEFGZIZABCJSZKLZADEJSZKLZIZHZVCBDMSZFGZBEM
    SZFGZCEMSZFGZAVPVRJSZKLAVRVTJSZKLAVPVTJSKLVCVDVEVIVNUAZVOVDVGVQVCVDVEVIVNUB
    ZVFVGVHVNUCBDNOVOVDVHVSWEVFVGVHVNUDZBENOVOVEVHWAVCVDVEVIVNUEZWFCENOVOABVLMS
    ZWBKVOVMAWHKLZVFVIVKVMUFVOVCVDVLFGZVMWIUGWDWEVIVFWJVNDEUKPABVLUHQVAVOBTGZDT
    GZETGZWHWBUIVFVIWKVNVDVCWKVEBRPUJZVIVFWLVNVGWLVHDRULPVIVFWMVNVHWMVGERUMPZBD
    EUNQUOVOAVJEMSZWCKVOVKAWPKLZVFVIVKVMUPVOVCVJFGZVHVKWQUGWDVOVDVEWRWEWGBCUKOW
    FAVJEUQQVAVOWKCTGZWMWPWCUIWNVFVIWSVNVEVCWSVDCRURUJWOBCEUSQUOAVPVRVTUTVB $.
    $( [1-Oct-2014] $)

  $( Congruence mod ` A ` is a symmetric/commutative relation.  (Contributed by
     Stefan O'Rear, 1-Oct-2014.) $)
  congsym $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. ZZ /\ A || ( B - C ) ) )
      -> A || ( C - B ) ) $=
    ( cz wcel wa cmin co cdivides wbr cneg simprr cc wceq zcn ad2antrl ad2antlr
    negsubdi2 syl2anc breqtrrd wb simpll simprl simplr zsubcl dvdsnegb mpbird )
    ADEZBDEZFZCDEZABCGHZIJZFZFZACBGHZIJZAUPKZIJZUOAULURIUJUKUMLUOCMEZBMEZURULNU
    KUTUJUMCOPUIVAUHUNBOQCBRSTUOUHUPDEZUQUSUAUHUIUNUBUOUKUIVBUJUKUMUCUHUIUNUDCB
    UESAUPUFSUG $.
    $( [1-Oct-2014] $)

  $( If two integers are congruent mod ` A ` , so are their negatives.
     (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
  congneg $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. ZZ /\ A || ( B - C ) ) )
      -> A || ( -u B - -u C ) ) $=
    ( cz wcel wa cmin co cdivides wbr cneg congsym wceq cc zcn syl2an ad2ant2lr
    neg2sub breqtrrd ) ADEZBDEZFCDEZABCGHIJZFFACBGHZBKCKGHZIABCLUAUBUEUDMZTUCUA
    BNECNEUFUBBOCOBCRPQS $.
    $( [1-Oct-2014] $)

  $( If two pairs of numbers are componentwise congruent, so are their
     differences.  (Contributed by Stefan O'Rear, 2-Oct-2014.) $)
  congsub $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) /\ ( D e. ZZ /\ E e. ZZ )
      /\ ( A || ( B - C ) /\ A || ( D - E ) ) ) -> A || ( ( B - D ) - ( C - E )
      ) ) $=
    ( cz wcel w3a wa cmin co cdivides wbr cneg caddc znegcl syl cc zsscn sseldi
    simp11 simp12 simp13 simp2l simp2r simp3l simp3r congneg syl22anc syl322anc
    congadd wceq negsub syl2anc oveq12d breqtrd ) AFGZBFGZCFGZHZDFGZEFGZIZABCJK
    LMZADEJKLMZIZHZABDNZOKZCENZOKZJKZBDJKZCEJKZJKLVGUQURUSVHFGZVJFGZVDAVHVJJKLM
    ZAVLLMUQURUSVCVFUAZUQURUSVCVFUBZUQURUSVCVFUCZVGVAVOUTVAVBVFUDZDPQVGVBVPUTVA
    VBVFUEZEPQUTVCVDVEUFVGUQVAVBVEVQVRWAWBUTVCVDVEUGADEUHUIABCVHVJUKUJVGVIVMVKV
    NJVGBRGDRGVIVMULVGFRBSVSTVGFRDSWATBDUMUNVGCRGERGVKVNULVGFRCSVTTVGFRESWBTCEU
    MUNUOUP $.
    $( [2-Oct-2014] $)

  $( Every integer is congruent to itself mod every base.  (Contributed by
     Stefan O'Rear, 1-Oct-2014.) $)
  congid $p |- ( ( A e. ZZ /\ B e. ZZ ) -> A || ( B - B ) ) $=
    ( cz wcel wa cc0 cmin co cdivides wbr dvds0 adantr cc wceq zcn adantl subid
    syl breqtrrd ) ACDZBCDZEZAFBBGHZITAFIJUAAKLUBBMDZUCFNUAUDTBOPBQRS $.
    $( [1-Oct-2014] $)

  ${
    $d F a b c $.  $d X a b c k $.  $d V a b c k $.  $d Y a b c k $.
    $d N a b c k $.

    $( Polynomials commute with congruences.  (Does this characterize them?)
       (Contributed by Stefan O'Rear, 5-Oct-2014.) $)
    mzpcong $p |- ( ( F e. ( mzPoly ` V ) /\ ( X e. ( ZZ ^m V ) /\ Y e. ( ZZ ^m
        V ) ) /\ ( N e. ZZ /\ A. k e. V N || ( ( X ` k ) - ( Y ` k ) ) ) ) -> N
        || ( ( F ` X ) - ( F ` Y ) ) ) $=
      ( vc cfv wcel cz cmin cdivides wbr cvv syl2anc wceq oveq12d breq2d fveq1
      co va vb cmzp cmap wa cv wral w3a cdm elfvdm dmmzp syl6eleq 3anim1i simp1
      csn cxp cmpt caddc cof cmul simpl3l simpr congid simpl2l vex fvconst2 syl
      simpl2r breqtrrd simpl3r weq fveq2 rcla4va eqid fvex fvmpt simp13l simp2l
      wf simp12l ffvelrn simp12r simp3l simp2r simp3r congadd syl322anc wfn ffn
      ovex a1i fnfvof syl22anc congmul mzpindd ) BDUCHIZEJDUDTZIZFWQIZUEZCJIZCA
      UFZEHZXBFHZKTZLMZADUGZUEZUHDNIZWTXHUHZWPCEBHZFBHZKTZLMZWPXIWTXHWPDUCUINBD
      UCUJUKULUMWPWTXHUNXJCEUAUFZHZFXOHZKTZLMCEWQUBUFZUOUPZHZFXTHZKTZLMCEGWQXSG
      UFZHZUQZHZFYFHZKTZLMCEXSHZFXSHZKTZLMZCEYDHZFYDHZKTZLMZCEXSYDURUSTZHZFYRHZ
      KTZLMCEXSYDUTUSTZHZFUUBHZKTZLMXNUABUBGDXJXSJIZUEZCXSXSKTZYCLUUGXAUUFCUUHL
      MXAXGXIWTUUFVAXJUUFVBCXSVCOUUGYAXSYBXSKUUGWRYAXSPWRWSXIXHUUFVDWQXSEUBVEZV
      FVGUUGWSYBXSPWRWSXIXHUUFVHWQXSFUUIVFVGQVIXJXSDIZUEZCXSEHZXSFHZKTZYILUUKUU
      JXGCUUNLMZXJUUJVBXAXGXIWTUUJVJXFUUOAXSDAUBVKZXEUUNCLUUPXCUULXDUUMKXBXSEVL
      XBXSFVLQRVMOUUKYGUULYHUUMKUUKWRYGUULPWRWSXIXHUUJVDGEYEUULWQYFXSYDESYFVNZX
      SEVOVPVGUUKWSYHUUMPWRWSXIXHUUJVHGFYEUUMWQYFXSYDFSUUQXSFVOVPVGQVIXJWQJXSVS
      ZYMUEZWQJYDVSZYQUEZUHZCYJYNURTZYKYOURTZKTZUUALUVBXAYJJIZYKJIZYNJIZYOJIZYM
      YQCUVELMXAXGXIWTUUSUVAVQZUVBUURWRUVFXJUURYMUVAVRZWRWSXIXHUUSUVAVTZWQJEXSW
      AOZUVBUURWSUVGUVKWRWSXIXHUUSUVAWBZWQJFXSWAOZUVBUUTWRUVHXJUUSUUTYQWCZUVLWQ
      JEYDWAOZUVBUUTWSUVIUVPUVNWQJFYDWAOZXJUURYMUVAWDZXJUUSUUTYQWEZCYJYKYNYOWFW
      GUVBYSUVCYTUVDKUVBXSWQWHZYDWQWHZWQNIZWRYSUVCPUVBUURUWAUVKWQJXSWIVGZUVBUUT
      UWBUVPWQJYDWIVGZUWCUVBJDUDWJWKZUVLWQURXSYDNEWLWMUVBUWAUWBUWCWSYTUVDPUWDUW
      EUWFUVNWQURXSYDNFWLWMQVIUVBCYJYNUTTZYKYOUTTZKTZUUELUVBXAUVFUVGUVHUVIYMYQC
      UWILMUVJUVMUVOUVQUVRUVSUVTCYJYKYNYOWNWGUVBUUCUWGUUDUWHKUVBUWAUWBUWCWRUUCU
      WGPUWDUWEUWFUVLWQUTXSYDNEWLWMUVBUWAUWBUWCWSUUDUWHPUWDUWEUWFUVNWQUTXSYDNFW
      LWMQVIXOXTPZXRYCCLUWJXPYAXQYBKEXOXTSFXOXTSQRXOYFPZXRYICLUWKXPYGXQYHKEXOYF
      SFXOYFSQRUAUBVKZXRYLCLUWLXPYJXQYKKEXOXSSFXOXSSQRUAGVKZXRYPCLUWMXPYNXQYOKE
      XOYDSFXOYDSQRXOYRPZXRUUACLUWNXPYSXQYTKEXOYRSFXOYRSQRXOUUBPZXRUUECLUWOXPUU
      CXQUUDKEXOUUBSFXOUUBSQRXOBPZXRXMCLUWPXPXKXQXLKEXOBSFXOBSQRWOO $.
      $( [5-Oct-2014] $)
  $}

  ${
    $d A a $.  $d N a $.
    $( Every integer is congruent to some number in the fundamental domain.
       (Contributed by Stefan O'Rear, 2-Oct-2014.) $)
    congrep $p |- ( ( A e. NN /\ N e. ZZ ) -> E. a e. ( 0 ... ( A - 1 ) ) A ||
        ( a - N ) ) $=
      ( cn wcel cz wa cmo co cc0 c1 cmin cfz cdivides wbr ancoms adantr syl2anc
      cv cn0 wrex zmodfz nnz simpr nn0ssz zmodcl sseldi cdiv cr crp zre moddifz
      nnrp syl2anr wne wb nnne0 zsubcl divides2 syl3anc mpbird congsym syl22anc
      wceq oveq1 breq2d rcla4ev ) ADEZBFEZGZBAHIZJAKLIMIZEZAVKBLIZNOZACSZBLIZNO
      ZCVLUAVIVHVMBAUBPVJAFEZVIVKFEZABVKLIZNOZVOVHVSVIAUCQZVHVIUDZVJTFVKUEVIVHV
      KTEBAUFPUGZVJWBWAAUHIFEZVIBUIEAUJEWFVHBUKAUMBAULUNVJVSAJUOZWAFEZWBWFUPWCV
      HWGVIAUQQVJVIVTWHWDWEBVKURRAWAUSUTVAABVKVBVCVRVOCVKVLVPVKVDVQVNANVPVKBLVE
      VFVGR $.
      $( [2-Oct-2014] $)
  $}

  $( If two integers are congruent, they are either equal or separated by at
     least the congruence base.  (Contributed by Stefan O'Rear, 4-Oct-2014.) $)
  congabseq $p |- ( ( ( A e. NN /\ B e. ZZ /\ C e. ZZ ) /\ A || ( B - C ) ) ->
      ( ( abs ` ( B - C ) ) < A <-> B = C ) ) $=
    ( wcel cz w3a cmin co wbr wa cabs cfv clt wceq cc0 cr zcn 3ad2ant1 ad2antrr
    cc cn cdivides cle wn wb zsubcl 3adant1 abscl 3syl adantr nnre ltnle biimpa
    syl2anc wne nnz ad3antrrr simpr 3jca simpllr dvdsleabs sylc ex necon1bd mpd
    3ad2ant2 3ad2ant3 subeq0 mpbid oveq1 adantl subid eqtrd fveq2d syl6eq nngt0
    syl abs0 eqbrtrd impbida ) AUADZBEDZCEDZFZABCGHZUBIZJZWEKLZAMIZBCNZWGWIJZWE
    ONZWJWKAWHUCIZUDZWLWGWIWNWGWHPDZAPDZWIWNUEWDWOWFWDWEEDZWETDWOWBWCWQWABCUFUG
    ZWEQWEUHUIUJWDWPWFWAWBWPWCAUKRUJWHAULUNUMWKWMWEOWKWEOUOZWMWKWSJZAEDZWQWSFWF
    WMWTXAWQWSWDXAWFWIWSWAWBXAWCAUPRUQWDWQWFWIWSWRUQWKWSURUSWDWFWIWSUTAWEVAVBVC
    VDVEWKBTDZCTDZWLWJUEWDXBWFWIWBWAXBWCBQVFSWDXCWFWIWCWAXCWBCQVGZSBCVHUNVIWGWJ
    JZWHOAMXEWHOKLOXEWEOKXEWECCGHZOWJWEXFNWGBCCGVJVKXEXCXFONWDXCWFWJXDSCVLVQVMV
    NVRVOWDOAMIZWFWJWAWBXGWCAVPRSVSVT $.
    $( [4-Oct-2014] $)

