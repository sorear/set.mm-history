$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Extensions beyond function theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c $.
    $( The closed-set generator is a well-behaved function.  (Contributed by
       Stefan O'Rear, 1-Feb-2015.) $)
    fncld $p |- Clsd Fn Top $=
      ( va vb cuni cdif wcel cpw crab cvv ccld ctop wfn df-cld fnmpt uniex pwex
      cv vex rabex a1i mprg ) APZCZBPDUAEZBUBFZGZHEZIJKAJAJUEIHBALMUFUAJEUCBUDU
      BUAAQNORST $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d ph x y $.  $d F x y $.  $d S x y $.  $d T x y $.  $d A x y $.
    $d B x y $.  $d X x y $.  $d Y x y $.
    isghmd.x $e |- X = ( Base ` S ) $.
    isghmd.y $e |- Y = ( Base ` T ) $.
    isghmd.a $e |- A = ( +g ` S ) $.
    isghmd.b $e |- B = ( +g ` T ) $.
    isghmd.s $e |- ( ph -> S e. Grp ) $.
    isghmd.t $e |- ( ph -> T e. Grp ) $.
    isghmd.f $e |- ( ph -> F : X --> Y ) $.
    isghmd.l $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) -> ( F ` ( x A y ) ) =
          ( ( F ` x ) B ( F ` y ) ) ) $.
    $( Deduction for a group homomorphism.  (Contributed by Stefan O'Rear,
       4-Feb-2015.) $)
    isghmd $p |- ( ph -> F e. ( S GrpHom T ) ) $=
      ( co wcel cghm cgrp wa wf cv cfv wceq wral jca ralrimivva isghm mpbir2and
      wb syl ) AHFGUASTZFUBTZGUBTZUCZIJHUDZBUEZCUEZDSHUFUTHUFVAHUFESUGZCIUHBIUH
      ZUCZAUPUQOPUIAUSVCQAVBBCIIRUJUIAUQUOURVDUCUMPCBDEFGHUBIJKLMNUKUNUL $.
      $( [4-Feb-2015] $)
  $}

  ${
    $d ph x y $.  $d F x y $.  $d S x y $.  $d T x y $.  $d A x y $.
    $d B x y $.  $d X x y $.  $d J x y $.  $d N x y $.  $d K x y $.
    islmhmd.x $e |- X = ( Base ` S ) $.
    islmhmd.a $e |- A = ( vsca ` S ) $.
    islmhmd.b $e |- B = ( vsca ` T ) $.
    islmhmd.k $e |- K = ( Scalar ` S ) $.
    islmhmd.j $e |- J = ( Scalar ` T ) $.
    islmhmd.n $e |- N = ( Base ` K ) $.
    islmhmd.s $e |- ( ph -> S e. LMod ) $.
    islmhmd.t $e |- ( ph -> T e. LMod ) $.
    islmhmd.c $e |- ( ph -> J = K ) $.
    islmhmd.f $e |- ( ph -> F e. ( S GrpHom T ) ) $.
    islmhmd.l $e |- ( ( ph /\ ( x e. N /\ y e. X ) ) -> ( F ` ( x A y ) ) =
          ( x B ( F ` y ) ) ) $.
    $( Deduction for a module homomorphism.  (Contributed by Stefan O'Rear,
       4-Feb-2015.) $)
    islmhmd $p |- ( ph -> F e. ( S LMHom T ) ) $=
      ( clmhm co wcel clmod wa cghm wceq cv cfv wral w3a jca ralrimivva 3jca wb
      islmhm syl mpbir2and ) AHFGUDUEUFZFUGUFZGUGUFZUHZHFGUIUEUFZIJUJZBUKZCUKZD
      UEHULVHVIHULEUEUJZCLUMBKUMZUNZAVCVDSTUOAVFVGVKUBUAAVJBCKLUCUPUQAVDVBVEVLU
      HURTBCKFGLHJIDEUGPQRMNOUSUTVA $.
      $( [4-Feb-2015] $)
  $}

  ${
    $d U x y $.  $d T x y $.  $d S x y $.  $d X x y $.  $d F x y $.
    $d L x y $.
    reslmhm2.u $e |- U = ( T |`s X ) $.
    reslmhm2.l $e |- L = ( LSubSp ` T ) $.
    $( Expansion of the codomain of a homomorphism.  (Contributed by Stefan
       O'Rear, 3-Feb-2015.) $)
    reslmhm2 $p |- ( ( F e. ( S LMHom U ) /\ T e. LMod /\ X e. L ) ->
        F e. ( S LMHom T ) ) $=
      ( vx vy clmhm co wcel clmod w3a wa cfv 3ad2ant1 cvv eqid cghm csca cv cbs
      wceq cvsca wral lmhmlmod1 simp2 jca csubg lsssubg 3adant1 resghm2 syl2anc
      lmghm cress ovex eqeltri resssca lmhmsca ressvsca lmhmlin ralrimivva 3jca
      mpan2 wb islmhm syl mpbir2and ) DACKLMZBNMZFEMZOZDABKLMZANMZVLPZDABUALMZB
      UBQZAUBQZUEZIUCZJUCZAUFQZLDQWBWCDQBUFQZLUEZJAUDQZUGIVTUDQZUGZOZVNVPVLVKVL
      VPVMACDUHRVKVLVMUIZUJVNVRWAWIVNDACUALMZFBUKQMZVRVKVLWLVMACDUPRVLVMWMVKEFB
      HULUMABCDFGUNUOVKVLWAVMVKCSMZWACBFUQLSGBFUQURUSZACDVTVSSVTTZFVSBCGVSTZUTV
      AVFRVKVLWIVMVKWNWIWOVKWNPWFIJWHWGWHACWGDVTWDWESWBWCWPWHTZWGTZWDTZFWEBCGWE
      TZVBVCVDVFRVEVNVLVOVQWJPVGWKIJWHABWGDVTVSWDWENWPWQWRWSWTXAVHVIVJ $.
      $( [3-Feb-2015] $)

    $( Expansion of the codomain of a homomorphism.  (Contributed by Stefan
       O'Rear, 3-Feb-2015.) $)
    reslmhm2b $p |- ( ( T e. LMod /\ X e. L /\ ran F C_ X ) ->
        ( F e. ( S LMHom T ) <-> F e. ( S LMHom U ) ) ) $=
      ( vx vy clmod wcel co wa csca cfv cvsca syl2anc eqid cvv crn wss w3a cghm
      clmhm wceq cv cbs wral lmhmlmod1 adantl simpl1 simpl2 lsslmod lmghm csubg
      jca simp1 simp2 lsssubg simp3 resghm2b biimpa syldan simpr resssca eqcomi
      wb lmhmsca ressvsca lmhmlin ralrimivva 3jca ovex eqeltri islmhm mpbir2and
      cress mp1i reslmhm2 syl3anc impbida ) BKLZFELZDUAFUBZUCZDABUEMLZDACUEMLZW
      FWGNZWHAKLZCKLZNZDACUDMLZCOPZAOPZUFZIUGZJUGZAQPZMDPWQWRDPCQPZMUFZJAUHPZUI
      IWOUHPZUIZUCZWIWJWKWGWJWFABDUJUKWIWCWDWKWCWDWEWGULZWCWDWEWGUMEFBCGHUNRUQW
      IWMWPXDWFWGDABUDMLZWMWGXGWFABDUOUKWFXGWMWFFBUPPLZWEXGWMVHWFWCWDXHWCWDWEUR
      WCWDWEUSEFBHUTRWCWDWEVAABCDFGVBRVCVDWIWGWCWPWFWGVEZXFABDWOWNKWOSZBOPZWNFX
      KBCGXKSVFVGVIRWIWGWCXDXIXFWGWCNXAIJXCXBXCABXBDWOWSWTKWQWRXJXCSZXBSZWSSZBQ
      PZWTFXOBCGXOSVJVGVKVLRVMCTLWHWLXENVHWICBFVRMTGBFVRVNVOIJXCACXBDWOWNWSWTTX
      JWNSXLXMXNWTSVPVSVQWFWHNWHWCWDWGWFWHVEWCWDWEWHULWCWDWEWHUMABCDEFGHVTWAWB
      $.
      $( [3-Feb-2015] $)
  $}

  ${
    $d A k x y z $.  $d B k x y z $.  $d ph k x y z $.  $d T k x y z $.
    $d Y x y z $.  $d P k x y z $.  $d S k x y z $.  $d K k x y z $.
    $d X k x y z $.  $d Z k x z y $.  $d R y $.
    gsumvsmul.b $e |- B = ( Base ` R ) $.
    gsumvsmul.s $e |- S = ( Scalar ` R ) $.
    gsumvsmul.k $e |- K = ( Base ` S ) $.
    gsumvsmul.z $e |- Z = ( 0g ` R ) $.
    gsumvsmul.p $e |- P = ( +g ` R ) $.
    gsumvsmul.t $e |- T = ( vsca ` R ) $.
    gsumvsmul.r $e |- ( ph -> R e. LMod ) $.
    gsumvsmul.a $e |- ( ph -> A e. V ) $.
    gsumvsmul.x $e |- ( ph -> X e. K ) $.
    gsumvsmul.y $e |- ( ( ph /\ k e. A ) -> Y e. B ) $.
    gsumvsmul.n $e |- ( ph -> ( `' ( k e. A |-> Y ) "
            ( _V \ { Z } ) ) e. Fin ) $.
    $( Pull a scalar multiplication out of a sum of vectors. _EDITORIAL_:
       properly generalizes ~ gsummulc2 , since every ring is a left module
       over itself.  (Contributed by Stefan O'Rear, 6-Feb-2015.) $)
    gsumvsmul $p |- ( ph -> ( R gsum ( k e. A |-> ( X T Y ) ) ) =
          ( X T ( R gsum ( k e. A |-> Y ) ) ) ) $=
      ( vy vx vz cv co cmpt ccom cgsu cfv clmod wcel ccmn lmodcmn syl wa adantr
      simpr lmodvscl syl3anc eqid fmptd cgrp wceq lmodgrp oveq2 ovex fvmpt 3syl
      grpidcl lmodvs0 syl2anc eqtrd lmodvsdi1 lmodvacl oveqan12d adantl 3eqtr4d
      3expb sylan gsumhomo eqidd fmptco oveq2d gsumcl 3eqtr3d ) AEUECKUEUHZGUIZ
      UJZHBLUJZUKZULUIEWMULUIZWLUMZEHBKLGUIZUJZULUIKWOGUIZAUFUGBCCDDWMEEWLMJMNN
      QQRRAEUNUOZEUPUOTEUQURZXAUAAUECWKCWLAWJCUOZUSWTKIUOZXBWKCUOAWTXBTUTAXCXBU
      BUTAXBVAKGFICEWJNOSPVBVCWLVDZVEAMWLUMZKMGUIZMAEVFUOZMCUOXEXFVGAWTXGTEVHUR
      CEMNQVMUEMWKXFCWLWJMKGVIXDKMGVJVKVLAWTXCXFMVGTUBGFIEKMOSPQVNVOVPAUFUHZCUO
      ZUGUHZCUOZUSZUSZKXHXJDUIZGUIZKXHGUIZKXJGUIZDUIZXNWLUMZXHWLUMZXJWLUMZDUIZX
      MWTXCXLXOXRVGAWTXLTUTAXCXLUBUTAXLVADKGFICEXHXJNROSPVQVCXMXNCUOZXSXOVGAWTX
      LYCTWTXIXKYCDCEXHXJNRVRWBWCUEXNWKXOCWLWJXNKGVIXDKXNGVJVKURXLYBXRVGAXIXKXT
      XPYAXQDUEXHWKXPCWLWJXHKGVIXDKXHGVJVKUEXJWKXQCWLWJXJKGVIXDKXJGVJVKVSVTWAAH
      BLCWMUCWMVDVEZUDWDAWNWREULAHUEBCLWKWQWMWLUCAWMWEAWLWEWJLKGVIWFWGAWOCUOWPW
      SVGABCWMEJMNQXAUAYDUDWHUEWOWKWSCWLWJWOKGVIXDKWOGVJVKURWI $.
      $( [6-Feb-2015] $)
  $}

  ${
    $d ph a $.  $d B a $.  $d Z a $.  $d G a $.  $d A a $.  $d V a $.
    $d X a $.  $d F a $.
    gsumpt.b $e |- B = ( Base ` G ) $.
    gsumpt.z $e |- Z = ( 0g ` G ) $.
    gsumpt.g $e |- ( ph -> G e. CMnd ) $.
    gsumpt.a $e |- ( ph -> A e. V ) $.
    gsumpt.x $e |- ( ph -> X e. A ) $.
    gsumpt.f $e |- ( ph -> F : A --> B ) $.
    gsumpt.s $e |- ( ph -> ( `' F " ( _V \ { Z } ) ) C_ { X } ) $.
    $( Sum of a family that is nonzero at at most one point.  (Contributed by
       Stefan O'Rear, 7-Feb-2015.) $)
    gsumpt $p |- ( ph -> ( G gsum F ) = ( F ` X ) ) $=
      ( va csn cgsu co wcel cres cfv cmpt feqmptd reseq1d wss wceq snssd resmpt
      cv syl eqtrd oveq2d cfn ccnv cvv cdif cima snfi ssfi sylancr gsumres ccmn
      wf ffvelrn syl2anc fveq2 gsumsn syl3anc 3eqtr3d ) AEDGQZUAZRSEPVKPUJZDUBZ
      UCZRSZEDRSGDUBZAVLVOERAVLPBVNUCZVKUAZVOADVRVKAPBCDNUDUEAVKBUFVSVOUGAGBMUH
      PBVKVNUIUKULUMABCDEFVKHIJKLNOAVKUNTDUOUPHQUQURZVKUFVTUNTGUSOVKVTUTVAVBAEV
      CTGBTZVQCTZVPVQUGKMABCDVDWAWBNMBCGDVEVFVNCVQPEGBIVMGDVGVHVIVJ $.
      $( [7-Feb-2015] $)
  $}


