$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    X and Y sequences 1: Definition and recurrence laws
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c rmX rmY $.

  $( Extend class notation to include the Robertson-Matiyasevich X sequence. $)
  crmx $a class rmX $.
  $( Extend class notation to include the Robertson-Matiyasevich Y sequence. $)
  crmy $a class rmY $.

  ${
    $d a n b c $.
    $( Define the X sequence as the rational part of some solution of a special
       Pell equation.  See ~ frmx and ~ rmxyval for a more useful but
       non-eliminable definition. $)
    df-rmx $a |- rmX = ( a e. ( ZZ>= ` 2 ) , n e. ZZ |-> ( 1st ` ( `' ( b e. (
        NN0 X. ZZ ) |-> ( ( 1st ` b ) + ( ( sqr ` ( ( a ^ 2 ) - 1 ) ) x. ( 2nd
        ` b ) ) ) ) ` ( ( a + ( sqr ` ( ( a ^ 2 ) - 1 ) ) ) ^ n ) ) ) ) $.
    $( Define the X sequence as the irrational part of some solution of a
       special Pell equation.  See ~ frmy and ~ rmxyval for a more useful but
       non-eliminable definition. $)
    df-rmy $a |- rmY = ( a e. ( ZZ>= ` 2 ) , n e. ZZ |-> ( 2nd ` ( `' ( b e. (
        NN0 X. ZZ ) |-> ( ( 1st ` b ) + ( ( sqr ` ( ( a ^ 2 ) - 1 ) ) x. ( 2nd
        ` b ) ) ) ) ` ( ( a + ( sqr ` ( ( a ^ 2 ) - 1 ) ) ) ^ n ) ) ) ) $.
  $}

  ${
    $d a n b A $.  $d a n b N $.
    $( Value of the X sequence.  Not used after ~ rmxyval is proved.
       (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
    rmxfval $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmX N ) = ( 1st ` (
        `' ( b e. ( NN0 X. ZZ ) |-> ( ( 1st ` b ) + ( ( sqr ` ( ( A ^ 2 ) - 1 )
        ) x. ( 2nd ` b ) ) ) ) ` ( ( A + ( sqr ` ( ( A ^ 2 ) - 1 ) ) ) ^ N ) )
        ) ) $=
      ( va vn c2 cfv cz cv cexp co c1 cmin csqr caddc c1st cmul cmpt ccnv wceq
      cuz cn0 cxp c2nd wa oveq1 oveq1d fveq2d oveq2d mpteq2dv cnveqd adantr id1
      crmx oveq12d oveqan12d fveq12d df-rmx fvex ovmpt2a ) DEABFUAGHDIZVAFJKZLM
      KZNGZOKZEIZJKZCUBHUCZCIZPGZVDVIUDGZQKZOKZRZSZGZPGAAFJKZLMKZNGZOKZBJKZCVHV
      JVSVKQKZOKZRZSZGZPGUNVAATZVFBTZUEZVPWFPWIVGWAVOWEWGVOWETWHWGVNWDWGCVHVMWC
      WGVLWBVJOWGVDVSVKQWGVCVRNWGVBVQLMVAAFJUFUGUHZUGUIUJUKULWGWHVEVTVFBJWGVAAV
      DVSOWGUMWJUOWHUMUPUQUHEDCURWFPUSUT $.
      $( [21-Sep-2014] $)

    $( Value of the Y sequence.  Not used after ~ rmxyval is proved.
       (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
    rmyfval $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmY N ) = ( 2nd ` (
        `' ( b e. ( NN0 X. ZZ ) |-> ( ( 1st ` b ) + ( ( sqr ` ( ( A ^ 2 ) - 1 )
        ) x. ( 2nd ` b ) ) ) ) ` ( ( A + ( sqr ` ( ( A ^ 2 ) - 1 ) ) ) ^ N ) )
        ) ) $=
      ( va vn c2 cfv cz cv cexp co c1 cmin csqr caddc c2nd cmul cmpt ccnv wceq
      cuz cn0 cxp c1st wa oveq1 oveq1d fveq2d oveq2d mpteq2dv cnveqd adantr id1
      crmy oveq12d id oveqan12d fveq12d df-rmy fvex ovmpt2a ) DEABFUAGHDIZVBFJK
      ZLMKZNGZOKZEIZJKZCUBHUCZCIZUDGZVEVJPGZQKZOKZRZSZGZPGAAFJKZLMKZNGZOKZBJKZC
      VIVKVTVLQKZOKZRZSZGZPGUNVBATZVGBTZUEZVQWGPWJVHWBVPWFWHVPWFTWIWHVOWEWHCVIV
      NWDWHVMWCVKOWHVEVTVLQWHVDVSNWHVCVRLMVBAFJUFUGUHZUGUIUJUKULWHWIVFWAVGBJWHV
      BAVEVTOWHUMWKUOWIUPUQURUHEDCUSWGPUTVA $.
      $( [21-Sep-2014] $)
  $}

  $( The discriminant used to define the X and Y sequences has an irrational
     square root.  (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
  rmspecsqrnq $p |- ( A e. ( ZZ>= ` 2 ) -> ( sqr ` ( ( A ^ 2 ) - 1 ) ) e. ( CC
      \ QQ ) ) $=
    ( c2 wcel cexp co c1 cmin cc cq 3syl ax-1cn sylancl syl clt caddc cmul wceq
    wbr cr a1i cuz cfv csqr wn cdif cz eluzelz zcn sqcl subcl sqrcl cn0 eluz2b2
    cn wa biimpi simpld nnsqcl nnm1nn0 eluzelre recnd binom2sub 2re 1re remulcl
    sylancr resqcli recni subsub syl3anc eqtr4d 2timesi simprd cc0 wb syl112anc
    2pos ltmul2 mpbid syl5eqbrr ltaddsub mulid1 oveq2d oveq12d breqtrrd resubcl
    sq1 nnre ltsub2 eqbrtrd ltm1 npcan oveq1d nonsq syl22anc eldif sylanbrc ) A
    BUAUBCZABDEZFGEZUCUBZHCZXAICUDZXAHIUECWRWTHCZXBWRWSHCZFHCZXDWRAUFCAHCZXEBAU
    GAUHAUIJZKWSFUJLWTUKMWRWTULCZAFGEZULCZXJBDEZWTNRWTXJFOEZBDEZNRXCWRAUNCZWSUN
    CZXIWRXOFANRZWRXOXQUOAUMUPZUQZAURZWSUSJWRXOXKXSAUSMWRXLWSBAFPEZPEZFBDEZGEZG
    EZWTNWRXLWSYBGEYCOEZYEWRXGXFXLYFQWRABAUTZVAZKAFVBLWRXEYBHCYCHCZYEYFQXHWRYBW
    RBSCZYASCZYBSCZVCWRASCZFSCZYKYGVDAFVELBYAVEVFZVAYIWRYCFVDVGZVHTWSYBYCVIVJVK
    WRFYDNRZYEWTNRZWRFBAPEZFGEZYDNWRFFOEZYSNRZFYTNRZWRUUABFPEZYSNFKVLWRXQUUDYSN
    RZWRXOXQXRVMWRYNYMYJVNBNRZXQUUEVOYNWRVDTZYGYJWRVCTUUFWRVQTFABVRVPVSVTWRYNYN
    YSSCZUUBUUCVOUUGUUGWRYJYMUUHVCYGBAVEVFFFYSWAVJVSWRYBYSYCFGWRYAABPWRXGYAAQYH
    AWBMWCYCFQWRWGTWDWEWRYNYDSCZWSSCZYQYRVOUUGWRYLYCSCUUIYOYPYBYCWFLWRXOXPUUJXS
    XTWSWHJZFYDWSWIVJVSWJWRWTWSXNNWRUUJWTWSNRUUKWSWKMWRXMABDWRXGXFXMAQYHKAFWLLW
    MWEWTXJWNWOXAHIWPWQ $.
    $( [21-Sep-2014] $)

  ${
    $d a A $.
    $( The discriminant used to define the X and Y sequences is a nonsquare
       positive integer and thus a valid Pell equation discriminant.
       (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
    rmspecnonsq $p |- ( A e. ( ZZ>= ` 2 ) -> ( ( A ^ 2 ) - 1 ) e. ( NN \ []NN )
        ) $=
      ( va c2 cfv wcel cexp co c1 cn csquarenn wn cdif cz cc0 clt wbr a1i cr cq
      csqr cuz cmin cn0 2nn0 eluznn0 mpan nn0z zsqcl 3syl 1z zsubcl syl2anc sq1
      eluz2b2 simprbi cle 1re 1nn0 nn0ge0i eluzelre nn0ge0 lt2sq syl22anc mpbid
      wb syl syl5eqbrr nn0re resqcl posdif elnnz sylanbrc wa rmspecsqrnq eldifn
      cc intnand crab df-squarenn eleq2i fveq2 eleq1d elrab bitr2i sylnib eldif
      cv wceq ) ACUADEZACFGZHUBGZIEZWKJEZKWKIJLEWIWKMEZNWKOPZWLWIWJMEZHMEZWNWIA
      UCEZAMEWPCUCEWIWRUDACUEUFZAUGAUHUIWQWIUJQWJHUKULWIHWJOPZWOWIHHCFGZWJOUMWI
      HAOPZXAWJOPZWIAIEXBAUNUOWIHREZNHUPPZAREZNAUPPZXBXCVEXDWIUQQZXEWIHURUSQCAU
      TWIWRXGWSAVAVFHAVBVCVDVGWIXDWJREZWTWOVEXHWIWRXFXIWSAVHAVIUIHWJVJULVDWKVKV
      LWIWLWKTDZSEZVMZWMWIXKWLWIXJVPSLEXKKAVNXJVPSVOVFVQWMWKBWGZTDZSEZBIVRZEXLJ
      XPWKBVSVTXOXKBWKIXMWKWHXNXJSXMWKTWAWBWCWDWEWKIJWFVL $.
      $( [21-Sep-2014] $)
  $}

  $( This lemma implements the concept of "equate rational and irrational
     parts", used to prove many arithmetical properties of the X and Y
     sequences.  (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
  qirropth $p |- ( ( A e. ( CC \ QQ ) /\ ( B e. QQ /\ C e. QQ ) /\ ( D e. QQ /\
      E e. QQ ) ) -> ( ( B + ( A x. C ) ) = ( D + ( A x. E ) ) <-> ( B = D /\ C
      = E ) ) ) $=
    ( cc cq wcel wa cmul co caddc wceq adantr cmin syl ad2antrr qcn syl2anc wb
    cdif w3a wn eldifn 3ad2ant1 cdiv simpll1 eldifi simp2r simp3r subdi syl3anc
    qsubcl mulcom simplr simp2l mulcl simp3l addsubeq4 syl22anc 3eqtr4d cc0 wne
    mpbid subeq0 necon3abid mpbird divmul syl112anc qdivcl eqeltrrd mt3d eqcomd
    simpr ex oveq2d eqtrd simpl2l simpl3l simpl1 simpl3r addcan2 jcai ancomd id
    oveq2 oveqan12d impbid1 ) AFGUAHZBGHZCGHZIZDGHZEGHZIZUBZBACJKZLKZDAEJKZLKZM
    ZBDMZCEMZIZWPXAXDWPXAIZXCXBXEXCXBXEXCAGHZWPXFUCZXAWIWLXGWOAFGUDUENXEXCUCZXF
    XEXHIZDBOKZCEOKZUFKZAGXIXLAMZXKAJKZXJMZXIAXKJKZWQWSOKZXNXJXIAFHZCFHZEFHZXPX
    QMXIWIXRWIWLWOXAXHUGAFGUHZPZXIWKXSWPWKXAXHWIWJWKWOUIQZCRPZXIWNXTWPWNXAXHWIW
    LWMWNUJQZERZPZACEUKULXIXKFHZXRXNXPMXIXKGHZYHXIWKWNYIYCYECEUMSZXKRPZYBXKAUNS
    XIXAXJXQMZWPXAXHUOXIBFHZWQFHZDFHZWSFHZXAYLTXIWJYMWPWJXAXHWIWJWKWOUPQZBRZPXI
    XRXSYNYBYDACUQSXIWMYOWPWMXAXHWIWLWMWNURQZDRZPXIXRXTYPYBYGAEUQZSBWQDWSUSUTVD
    VAXIXJFHZXRYHXKVBVCZXMXOTXIXJGHZUUBXIWMWJUUDYSYQDBUMSZXJRPYBYKXIUUCXHXEXHVN
    XIXSXTUUCXHTYDYGXSXTIXCXKVBCEVEVFSVGZXJAXKVHVIVGXIUUDYIUUCXLGHUUEYJUUFXJXKV
    JULVKVOVLXEXCXBXEXCIZBWSLKZWTMZXBUUGUUHWRWTUUGWSWQBLUUGECAJUUGCEXEXCVNVMVPV
    PWPXAXCUOVQUUGYMYOYPUUIXBTXEYMXCXEWJYMWJWKWIWOXAVRYRPNXEYOXCXEWMYOWMWNWIWLX
    AVSYTPNXEYPXCXEXRXTYPXEWIXRWIWLWOXAVTYAPXEWNXTWMWNWIWLXAWAYFPUUASNBDWSWBULV
    DVOWCWDVOXBXCBDWQWSLXBWECEAJWFWGWH $.
    $( [21-Sep-2014] $)

  $( The base of exponent used to define the X and Y sequences is the
     fundamental solution of the corresponding Pell equation.  (Contributed by
     Stefan O'Rear, 21-Sep-2014.) $)
  rmspecfund $p |- ( A e. ( ZZ>= ` 2 ) -> ( PellFund ` ( ( A ^ 2 ) - 1 ) ) = (
      A + ( sqr ` ( ( A ^ 2 ) - 1 ) ) ) ) $=
    ( c2 cfv wcel co c1 cmin csqr caddc wceq cle wbr clt cmul cc cr a1i syl2anc
    wb syl cuz cexp cpellfund cn csquarenn cpell14qr rmspecnonsq crp cz eluzelz
    cdif cc0 zre 3syl 1re resubcl sq1 eluz2b2 simprbi 1nn0 nn0ge0i eluzelre cn0
    zsqcl 2nn0 eluznn0 mpan nn0ge0 lt2sq syl22anc eqbrtrrd posdif elrp sylanbrc
    mpbid rpsqrcl recnd mulid1 oveq2d cpell1qr pell1qrss14 oveq2i syl5eq ax-1cn
    rpre wss nncan eqtrd pellqrexplicit syl31anc sseldd eqeltrrd readdcl ltadd1
    ltaddrp lttrd pellfundlb npcan fveq2d sqrsq oveq1d pellfundge wa pellfundre
    syl3anc letri3 mpbir2and ) ABUACDZABUBEZFGEZUCCZAXJHCZIEZJZXKXMKLZXMXKKLZXH
    XJUDUEUKDZXMXJUFCZDFXMMLXOAUGZXHAXLFNEZIEZXMXRXHXTXLAIXHXLODXTXLJXHXLXHXLUH
    DZXLPDZXHXJUHDZYBXHXJPDZULXJMLZYDXHXIPDZFPDZYEXHAUIDXIUIDYGBAUJAVDXIUMUNZYH
    XHUOQZXIFUPRZXHFXIMLZYFXHFBUBEZFXIMYMFJXHUQQXHFAMLZYMXIMLZXHAUDDYNAURUSZXHY
    HULFKLZAPDZULAKLZYNYOSYJYQXHFUTVAQBAVBZXHAVCDZYSBVCDXHUUAVEABVFVGZAVHTZFAVI
    VJVOVKXHYHYGYLYFSYJYIFXIVLRVOXJVMVNXJVPTZXLWETZVQXLVRTVSXHXJVTCZXRYAXHXQUUF
    XRWFXSXJWATXHXQUUAFVCDZXIXJYMNEZGEZFJYAUUFDXSUUBUUGXHUTQXHUUIXIXJGEZFXHUUHX
    JXIGXHUUHXJFNEZXJYMFXJNUQWBXHXJODUUKXJJXHXJYKVQXJVRTWCVSXHXIODZFODZUUJFJXHX
    IYIVQZUUMXHWDQZXIFWGRWHAFXJWIWJWKWLXHFFXLIEZXMYJXHYHYCUUPPDYJUUEFXLWMRXHYRY
    CXMPDZYTUUEAXLWMRZXHYHYBFUUPMLYJUUDFXLWORXHYNUUPXMMLZYPXHYHYRYCYNUUSSYJYTUU
    EFAXLWNXEVOWPXMXJWQXEXHXJFIEZHCZXLIEZXMXKKXHUVAAXLIXHUVAXIHCZAXHUUTXIHXHUUL
    UUMUUTXIJUUNUUOXIFWRRWSXHYRYSUVCAJYTUUCAWTRWHXAXHXQUVBXKKLXSXJXBTVKXHXKPDZU
    UQXNXOXPXCSXHXQUVDXSXJXDTUURXKXMXFRXG $.
    $( [21-Sep-2014] $)

  ${
    $d A a c d $.  $d N a $.
    $( The solutions used to construct the X and Y sequences are quadratic
       irrationals.  (Contributed by Stefan O'Rear, 21-Sep-2014.) $)
    rmxyelqirr $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( ( A + ( sqr ` ( (
        A ^ 2 ) - 1 ) ) ) ^ N ) e. { a | E. c e. NN0 E. d e. ZZ a = ( c + ( (
        sqr ` ( ( A ^ 2 ) - 1 ) ) x. d ) ) } ) $=
      ( c2 cfv wcel cz wa cexp co cv wceq wrex cn0 cr crab cvv wss c1 cpell14qr
      cuz cmin csqr cmul caddc cn csquarenn cdif rmspecnonsq adantr pell14qrval
      cab syl wral simpl reximi rgenw a1i ss2rab sylibr ssv rabss2 ax-mp syl6ss
      wi rabab syl6sseq eqsstrd cpellfund simpr rmspecfund eqcomd oveq1d eqeq2d
      oveq2 rcla4ev syl2anc wb pellfund14b mpbird sseldd ) AFUCGHZBIHZJZAFKLUAU
      DLZUBGZCMZDMZWGUEGZEMZUFLUGLNZEIOZDPOZCUNZAWKUGLZBKLZWFWHWMWJFKLWGWLFKLUF
      LUDLUANZJZEIOZDPOZCQRZWPWFWGUHUIUJHZWHXCNWDXDWEAUKULZCDEWGUMUOWFXCWOCSRZW
      PWFXCWOCQRZXFWFXBWOVGZCQUPZXCXGTXIWFXHCQXAWNDPWTWMEIWMWSUQURURUSUTXBWOCQV
      AVBQSTXGXFTQVCWOCQSVDVEVFWOCVHVIVJWFWRWHHZWRWGVKGZWIKLZNZCIOZWFWEWRXKBKLZ
      NZXNWDWEVLWFWQXKBKWFXKWQWDXKWQNWEAVMULVNVOXMXPCBIWIBNXLXOWRWIBXKKVQVPVRVS
      WFXDXJXNVTXECWRWGWAUOWBWC $.
      $( [21-Sep-2014] $)
  $}

  ${
    $d b c d a A $.
    $( The function used to extract rational and irrational parts in ~ df-rmx
       and ~ df-rmy in fact achieves a one-to-one mapping from the quadratic
       irrationals to pairs of integers.  (Contributed by Stefan O'Rear,
       21-Sep-2014.) $)
    rmxypairf1o $p |- ( A e. ( ZZ>= ` 2 ) -> ( b e. ( NN0 X. ZZ ) |-> ( ( 1st `
        b ) + ( ( sqr ` ( ( A ^ 2 ) - 1 ) ) x. ( 2nd ` b ) ) ) ) : ( NN0 X. ZZ
        ) -1-1-onto-> { a | E. c e. NN0 E. d e. ZZ a = ( c + ( ( sqr ` ( ( A ^
        2 ) - 1 ) ) x. d ) ) } ) $=
      ( cfv wcel cn0 cz cv c1st co c2nd cmul caddc wceq wrex fveq2 cq 3syl cexp
      c2 cuz cxp c1 cmin csqr cmpt wfn crn cab weq wi wral wf1o eqid fnmpt ovex
      cvv a1i mprg wb cop op1st syl6eq op2nd oveq2d oveq12d eqeq2d rexxp bicomi
      abbidv rnmpt syl6reqr wa fvmpt ad2antrl ad2antll eqeq12d cdif rmspecsqrnq
      vex adantr simprl xp1st nn0ssq sseli xp2nd simprr syl122anc biimpd xpopth
      cc zq qirropth adantl sylibd sylbid ralrimivva dff1o6 syl3anbrc ) AUBUCFG
      ZCHIUDZCJZKFZAUBUALUEUFLUGFZXDMFZNLZOLZUHZXCUIZXJUJZBJZDJZXFEJZNLZOLZPZEI
      QDHQZBUKZPXNXJFZXOXJFZPZDEULZUMZEXCUNDXCUNXCXTXJUOXKXBXIUSGZXKCXCCXCXIXJU
      SXJUPZUQYFXDXCGXEXHOURUTVAUTXBXTXMXIPZCXCQZBUKXLXBXSYIBXSYIVBXBYIXSYHXRCD
      EHIXDXNXOVCZPZXIXQXMYKXEXNXHXPOYKXEYJKFXNXDYJKRXNXODWBZVDVEYKXGXOXFNYKXGY
      JMFXOXDYJMRXNXOYLEWBVFVEVGVHVIVJVKUTVLCBXCXIXJYGVMVNXBYEDEXCXCXBXNXCGZXOX
      CGZVOZVOZYCXNKFZXFXNMFZNLZOLZXOKFZXFXOMFZNLZOLZPZYDYPYAYTYBUUDYMYAYTPXBYN
      CXNXIYTXCXJCDULZXEYQXHYSOXDXNKRUUFXGYRXFNXDXNMRVGVHYGYQYSOURVPVQYNYBUUDPX
      BYMCXOXIUUDXCXJCEULZXEUUAXHUUCOXDXOKRUUGXGUUBXFNXDXOMRVGVHYGUUAUUCOURVPVR
      VSYPUUEYQUUAPYRUUBPVOZYDYPUUEUUHYPXFWMSVTGZYQSGZYRSGZUUASGZUUBSGZUUEUUHVB
      XBUUIYOAWAWCYPYMYQHGUUJXBYMYNWDZXNHIWEHSYQWFWGTYPYMYRIGUUKUUNXNHIWHYRWNTY
      PYNUUAHGUULXBYMYNWIZXOHIWEHSUUAWFWGTYPYNUUBIGUUMUUOXOHIWHUUBWNTXFYQYRUUAU
      UBWOWJWKYOUUHYDVBXBXNXOHIHIWLWPWQWRWSDEXCXTXJWTXA $.
      $( [21-Sep-2014] $)
  $}

  ${
    $d a b c d A $.  $d a N $.
    $( Lemma for ~ frmx and ~ frmy . $)
    rmxyelxp $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( `' ( b e. ( NN0 X.
        ZZ ) |-> ( ( 1st ` b ) + ( ( sqr ` ( ( A ^ 2 ) - 1 ) ) x. ( 2nd ` b ) )
        ) ) ` ( ( A + ( sqr ` ( ( A ^ 2 ) - 1 ) ) ) ^ N ) ) e. ( NN0 X. ZZ ) )
        $=
      ( va vc vd c2 cuz cfv wcel cz wa cn0 cxp cv cexp co cmul caddc wrex cmin
      c1 csqr wceq cab c1st c2nd cmpt wf1o ccnv rmxypairf1o rmxyelqirr f1ocnvdm
      adantr syl2anc ) AGHIJZBKJZLMKNZDOEOAGPQUBUAQUCIZFORQSQUDFKTEMTDUEZCURCOZ
      UFIUSVAUGIRQSQUHZUIZAUSSQBPQZUTJVDVBUJIURJUPVCUQADCEFUKUNABDEFULURUTVDVBU
      MUO $.
      $( [22-Sep-2014] $)
  $}

  ${
    $d a b c $.
    $( The X sequence is a nonnegative integer.  See ~ rmxnn for a
       strengthening.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
    frmx $p |- rmX : ( ( ZZ>= ` 2 ) X. ZZ ) --> NN0 $=
      ( va vb vc cv c2 cexp co c1 cmin csqr cfv caddc cn0 cz cxp c1st c2nd wcel
      wral crmx cmul cmpt ccnv cuz wf wa rmxyelxp xp1st rgen2 df-rmx fmpt2 mpbi
      syl ) ADZUNEFGHIGJKZLGBDZFGCMNOZCDZPKUOURQKUAGLGUBUCKZPKZMRZBNSAEUDKZSVBN
      OMTUEVAABVBNUNVBRUPNRUFUSUQRVAUNUPCUGUSMNUHUMUIABVBNUTMTBACUJUKUL $.
      $( [22-Sep-2014] $)

    $( The Y sequence is an integer.  (Contributed by Stefan O'Rear,
       22-Sep-2014.) $)
    frmy $p |- rmY : ( ( ZZ>= ` 2 ) X. ZZ ) --> ZZ $=
      ( va vb vc cv c2 cexp co c1 cmin csqr cfv caddc cn0 cz cxp c1st c2nd wcel
      wral crmy cmul cmpt ccnv cuz wf wa rmxyelxp xp2nd rgen2 df-rmy fmpt2 mpbi
      syl ) ADZUNEFGHIGJKZLGBDZFGCMNOZCDZPKUOURQKUAGLGUBUCKZQKZNRZBNSAEUDKZSVBN
      ONTUEVAABVBNUNVBRUPNRUFUSUQRVAUNUPCUGUSMNUHUMUIABVBNUTNTBACUJUKUL $.
      $( [22-Sep-2014] $)
  $}

  ${
    $d a b c d A $.  $d a b c N $.
    $( Main definition of the X and Y sequences.  Compare definition 2.3 of
       [JonesMatijasevic] p. 694.  (Contributed by Stefan O'Rear,
       19-Oct-2014.) $)
    rmxyval $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( ( A rmX N ) + ( ( sqr
        ` ( ( A ^ 2 ) - 1 ) ) x. ( A rmY N ) ) ) = ( ( A + ( sqr ` ( ( A ^ 2 )
        - 1 ) ) ) ^ N ) ) $=
      ( vb va vc vd c2 cfv wcel cz co cmul caddc c1st c2nd oveq2d oveq12d fveq2
      cv wceq cuz wa crmx cexp cmin csqr crmy cn0 cxp cmpt ccnv rmxfval rmyfval
      c1 rmxyelxp weq cbvmptv ovex fvmpt syl wrex rmxypairf1o adantr rmxyelqirr
      cab wf1o f1ocnvfv2 syl2anc 3eqtr2d ) AGUAHIZBJIZUBZABUCKZAGUDKUNUEKUFHZAB
      UGKZLKZMKAVNMKBUDKZCUHJUIZCSZNHZVNVSOHZLKZMKZUJZUKHZNHZVNWEOHZLKZMKZWEWDH
      ZVQVLVMWFVPWHMABCULVLVOWGVNLABCUMPQVLWEVRIWJWITABCUODWEDSZNHZVNWKOHZLKZMK
      ZWIVRWDWKWETZWLWFWNWHMWKWENRWPWMWGVNLWKWEORPQCDVRWCWOCDUPZVTWLWBWNMVSWKNR
      WQWAWMVNLVSWKORPQUQWFWHMURUSUTVLVRWKESVNFSLKMKTFJVAEUHVADVEZWDVFZVQWRIWJV
      QTVJWSVKADCEFVBVCABDEFVDVRWRVQWDVGVHVI $.
      $( [19-Oct-2014] $)
  $}

  $( The discriminant used to define the X and Y sequences is a positive real.
     (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmspecpos $p |- ( A e. ( ZZ>= ` 2 ) -> ( ( A ^ 2 ) - 1 ) e. RR+ ) $=
    ( c2 cuz cfv wcel cexp co c1 cmin cr cc0 clt wbr crp cn0 a1i syl2anc cle wb
    mpbid 2nn0 eluznn0 mpan resqcl 3syl 1re resubcl sq1 cz eluz2b1 simprbi 1nn0
    nn0re nn0ge0i eluzelre nn0ge0 lt2sq syl22anc syl5eqbrr posdif elrp sylanbrc
    syl ) ABCDEZABFGZHIGZJEZKVFLMZVFNEVDVEJEZHJEZVGVDAOEZAJEZVIBOEVDVKUAABUBUCZ
    AUMAUDUEZVJVDUFPZVEHUGQVDHVELMZVHVDHHBFGZVELUHVDHALMZVQVELMZVDAUIEVRAUJUKVD
    VJKHRMZVLKARMZVRVSSVOVTVDHULUNPBAUOVDVKWAVMAUPVCHAUQURTUSVDVJVIVPVHSVOVNHVE
    UTQTVFVAVB $.
    $( [22-Sep-2014] $)

  ${
    $d A n $.  $d X n $.  $d Y n $.  $d X x y $.  $d Y x y $.  $d A x y $.
    $( The X and Y sequences taken together enumerate all solutions to the
       corresponding Pell equation in the right half-plane.  (Contributed by
       Stefan O'Rear, 22-Sep-2014.) $)
    rmxycomplete $p |- ( ( A e. ( ZZ>= ` 2 ) /\ X e. NN0 /\ Y e. ZZ ) -> ( ( (
        X ^ 2 ) - ( ( ( A ^ 2 ) - 1 ) x. ( Y ^ 2 ) ) ) = 1 <-> E. n e. ZZ ( X =
        ( A rmX n ) /\ Y = ( A rmY n ) ) ) ) $=
      ( vx vy c2 wcel cn0 cz cexp co c1 cmin cmul caddc wceq wa cq adantr cv cn
      cuz cfv w3a csqr cpell14qr cpellfund wrex crmx crmy csquarenn rmspecnonsq
      cdif wb 3ad2ant1 pellfund14b syl cr nn0re 3ad2ant2 rmspecpos rpsqrcl rpre
      crp 3syl zre 3ad2ant3 remulcl syl2anc biantrurd simpl2 simpl3 eqidd simpr
      readdcl oveq1 eqeq2d oveq1d eqeq1d anbi12d oveq2 rcla42ev syl112anc ex cc
      oveq2d rmspecsqrnq nn0ssq simp2 sseldi zq sseli ad2antrl syl122anc biimpd
      ad2antll anim1d oveqan12d eqcomd biimpa syl6 rexlimdvva impbid elpell14qr
      qirropth 3bitr4d cxp wf frmx a1i simpl1 fovrn syl3anc zssq frmy 3ad2antl1
      rmxyval rmspecfund eqtr4d bitr3d rexbidva ) AGUCUDZHZCIHZDJHZUEZCAGKLMNLZ
      UFUDZDOLZPLZYHUGUDHZYKYHUHUDZBUAZKLZQZBJUIZCGKLZYHDGKLZOLZNLZMQZCAYNUJLZQ
      DAYNUKLZQRZBJUIYGYHUBULUNHZYLYQUOYDYEUUFYFAUMUPZBYKYHUQURYGYKEUAZYIFUAZOL
      ZPLZQZUUHGKLZYHUUIGKLZOLZNLZMQZRZFJUIEIUIZYKUSHZUUSRZUUBYLYGUUTUUSYGCUSHZ
      YJUSHZUUTYEYDUVBYFCUTVAYGYIUSHZDUSHZUVCYDYEUVDYFYDYHVEHYIVEHUVDAVBYHVCYIV
      DVFUPYFYDUVEYEDVGVHYIDVIVJCYJVPVJVKYGUUBUUSYGUUBUUSYGUUBRZYEYFYKYKQZUUBUU
      SYDYEYFUUBVLYDYEYFUUBVMUVFYKVNYGUUBVOUURUVGUUBRYKCUUJPLZQZYRUUONLZMQZREFC
      DIJUUHCQZUULUVIUUQUVKUVLUUKUVHYKUUHCUUJPVQVRUVLUUPUVJMUVLUUMYRUUONUUHCGKV
      QVSVTWAUUIDQZUVIUVGUVKUUBUVMUVHYKYKUVMUUJYJCPUUIDYIOWBWGVRUVMUVJUUAMUVMUU
      OYTYRNUVMUUNYSYHOUUIDGKVQWGWGVTWAWCWDWEYGUURUUBEFIJYGUUHIHZUUIJHZRZRZUURC
      UUHQZDUUIQZRZUUQRUUBUVQUULUVTUUQUVQUULUVTUVQYIWFSUNHZCSHZDSHZUUHSHZUUISHZ
      UULUVTUOYGUWAUVPYDYEUWAYFAWHUPZTYGUWBUVPYGISCWIYDYEYFWJWKZTYGUWCUVPYFYDUW
      CYEDWLVHZTUVNUWDYGUVOISUUHWIWMWNUVOUWEYGUVNUUIWLWQYICDUUHUUIXFWOWPWRUVTUU
      QUUBUVTUUPUUAMUVTUUAUUPUVRUVSYRUUMYTUUONCUUHGKVQUVSYSUUNYHODUUIGKVQWGWSWT
      VTXAXBXCXDYGUUFYLUVAUOUUGEFYKYHXEURXGYGUUEYPBJYGYNJHZRZYKUUCYIUUDOLPLZQZU
      UEYPUWJUWAUWBUWCUUCSHUUDSHUWLUUEUOYGUWAUWIUWFTYGUWBUWIUWGTYGUWCUWIUWHTUWJ
      ISUUCWIUWJYCJXHZIUJXIZYDUWIUUCIHUWNUWJXJXKYDYEYFUWIXLZYGUWIVOZAYNIYCJUJXM
      XNWKUWJJSUUDXOUWJUWMJUKXIZYDUWIUUDJHUWQUWJXPXKUWOUWPAYNJYCJUKXMXNWKYICDUU
      CUUDXFWOUWJUWKYOYKUWJUWKAYIPLZYNKLZYOYDYEUWIUWKUWSQYFAYNXRXQUWJYMUWRYNKYG
      YMUWRQZUWIYDYEUWTYFAXSUPTVSXTVRYAYBXG $.
      $( [22-Sep-2014] $)
  $}

  ${
    $d A a $.  $d N a $.
    $( The X and Y sequences define a solution to the corresponding Pell
       equation.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
    rmxynorm $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( ( ( A rmX N ) ^ 2 )
        - ( ( ( A ^ 2 ) - 1 ) x. ( ( A rmY N ) ^ 2 ) ) ) = 1 ) $=
      ( va c2 cuz wcel cz wa crmx co cexp cmin crmy wceq eqidd oveq2 eqeq2d cn0
      c1 fovcl cmul cv wrex simpr anim12i anbi12d rcla4ev syl2anc wb simpl frmx
      cfv frmy rmxycomplete syl3anc mpbird ) ADEULZFZBGFZHZABIJZDKJADKJSLJABMJZ
      DKJUAJLJSNZVAACUBZIJZNZVBAVDMJZNZHZCGUCZUTUSVAVANZVBVBNZHZVJURUSUDURVKUSV
      LURVAOUSVBOUEVIVMCBGVDBNZVFVKVHVLVNVEVAVAVDBAIPQVNVGVBVBVDBAMPQUFUGUHUTUR
      VARFVBGFVCVJUIURUSUJABRUQGIUKTABGUQGMUMTACVAVBUNUOUP $.
      $( [22-Sep-2014] $)
  $}

  $( The base of exponentiation for the X and Y sequences is a positive real.
     (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmbaserp $p |- ( A e. ( ZZ>= ` 2 ) -> ( A + ( sqr ` ( ( A ^ 2 ) - 1 ) ) ) e.
      RR+ ) $=
    ( c2 cuz cfv wcel cexp co c1 cmin cpellfund csqr caddc rmspecfund csquarenn
    crp cn cdif rmspecnonsq pellfundrp syl eqeltrrd ) ABCDEZABFGHIGZJDZAUCKDLGO
    AMUBUCPNQEUDOEARUCSTUA $.
    $( [22-Sep-2014] $)

  $( Negation law for X and Y sequences.  JonesMatijasevic is inconsistent on
     whether the X and Y sequences have domain ` NN0 ` or ` ZZ ` ; we use
     ` ZZ ` consistently to avoid the need for a separate subtraction law.
     (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmxyneg $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( ( A rmX -u N ) = ( A
      rmX N ) /\ ( A rmY -u N ) = -u ( A rmY N ) ) ) $=
    ( c2 wcel cz crmx co cexp c1 cmin crmy cmul caddc cc syl sseldi syl2anc cn0
    wceq cq cuz cfv wa cneg csqr cdiv znegcl rmxyval sylan2 cc0 rmbaserp adantr
    wne crp rpcn rpne0 simpr expnegz syl3anc cn csquarenn cdif rmspecnonsq nncn
    eldifi 3syl sqrcl zsscn frmy fovcl mulneg2 oveq2d nn0sscn frmx mulcl negsub
    eqtrd subsq sqmul sqrth oveq1d 3eqtr2d expclz eqeltrd expne0i eqnetrd recid
    rmxynorm eqtr4d negcl addcl reccl mulcan syl112anc mpbid eqtr2d rmspecsqrnq
    wb 3eqtrd nn0ssq zssq qnegcl qirropth syl122anc ) ACUAUBZDZBEDZUCZABUDZFGZA
    CHGIJGZUEUBZAXIKGZLGMGZABFGZXLABKGZUDZLGZMGZSZXJXOSXMXQSUCZXHXNAXLMGZXIHGZI
    YBBHGZUFGZXSXGXFXIEDZXNYCSBUGZAXIUHUIXHYBNDZYBUJUMZXGYCYESXFYHXGXFYBUNDZYHA
    UKZYBUOOULZXFYIXGXFYJYIYKYBUPOULZXFXGUQZYBBURUSXHXSIXOXLXPLGZMGZUFGZYEXHYPX
    SLGZYPYQLGZSZXSYQSZXHYRIYSXHYRYPXOYOJGZLGZXOCHGZYOCHGZJGZIXHXSUUBYPLXHXSXOY
    OUDZMGZUUBXHXRUUGXOMXHXLNDZXPNDZXRUUGSXHXKNDZUUIXFUUKXGXFXKUTVAVBDXKUTDUUKA
    VCXKUTVAVEXKVDVFULZXKVGOZXHENXPVHABEXEEKVIVJZPZXLXPVKQVLXHXONDZYONDZUUHUUBS
    XHRNXOVMABRXEEFVNVJZPZXHUUIUUJUUQUUMUUOXLXPVOQZXOYOVPQVQVLXHUUPUUQUUFUUCSUU
    SUUTXOYOVRQXHUUFUUDXKXPCHGZLGZJGIXHUUEUVBUUDJXHUUEXLCHGZUVALGZUVBXHUUIUUJUU
    EUVDSUUMUUOXLXPVSQXHUVCXKUVALXHUUKUVCXKSUULXKVTOWAVQVLABWHVQWBXHYPNDZYPUJUM
    ZYSISXHYPYDNABUHZXHYHYIXGYDNDYLYMYNYBBWCUSWDZXHYPYDUJUVGXHYHYIXGYDUJUMYLYMY
    NYBBWEUSWFZYPWGQWIXHXSNDZYQNDZUVEUVFYTUUAWRXHUUPXRNDZUVJUUSXHUUIXQNDZUVLUUM
    XHUUJUVMUUOXPWJOXLXQVOQXOXRWKQXHUVEUVFUVKUVHUVIYPWLQUVHUVIXSYQYPWMWNWOXHYPY
    DIUFUVGVLWPWSXHXLNTVBDZXJTDXMTDXOTDXQTDZXTYAWRXFUVNXGAWQULXHRTXJWTXGXFYFXJR
    DYGAXIRXEEFVNVJUIPXHETXMXAXGXFYFXMEDYGAXIEXEEKVIVJUIPXHRTXOWTUURPXHXPTDUVOX
    HETXPXAUUNPXPXBOXLXJXMXOXQXCXDWO $.
    $( [22-Sep-2014] $)

  $( Addition formula for X and Y sequences.  See ~ rmxadd and ~ rmyadd for
     most uses.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmxyadd $p |- ( ( A e. ( ZZ>= ` 2 ) /\ M e. ZZ /\ N e. ZZ ) -> (
        ( A rmX ( M + N ) ) = ( ( ( A rmX M ) x. ( A rmX N ) ) + ( ( ( A ^ 2 )
      - 1 ) x. ( ( A rmY M ) x. ( A rmY N ) ) ) ) /\
        ( A rmY ( M + N ) ) = ( ( ( A rmY M ) x. ( A rmX N ) ) + ( ( A rmX M )
      x. ( A rmY N ) ) ) ) ) $=
    ( wcel cz caddc co crmx cexp crmy cmul wceq syl2anc cc cq cn0 fovrn syl3anc
    sseldi qmulcl c2 cuz cfv w3a c1 cmin csqr wa zaddcl 3adant1 rmxyval cc0 wne
    simp1 eluzelz 3ad2ant1 zcn syl zq qsqcl 3syl 1z sselii a1i qsubcl qcn sqrcl
    zssq addcl crp rmbaserp rpne0 simp2 simp3 expaddz syl22anc qsscn nn0ssq cxp
    frmx frmy mulcl muladd oveq12d mul4 sqval rmspecpos rpcn sqrth eqtr3d eqtrd
    wf mulcom oveq2d mul12 adddi addcom oveq1d 3eqtr2d 3eqtr3d cdif rmspecsqrnq
    3eqtrd wb qaddcl qirropth syl122anc mpbid ) AUAUBUCZDZBEDZCEDZUDZABCFGZHGZA
    UAIGZUEUFGZUGUCZAXNJGZKGFGZABHGZACHGZKGZXQABJGZACJGZKGZKGZFGZXRYDYBKGZYAYEK
    GZFGZKGZFGZLZXOYHLXSYKLUHZXMXTAXRFGZXNIGZYPBIGZYPCIGZKGZYMXMXJXNEDZXTYQLXJX
    KXLUNZXKXLUUAXJBCUIUJZAXNUKMXMYPNDZYPULUMZXKXLYQYTLXMANDZXRNDZUUDXMAEDZUUFX
    JXKUUHXLUAAUOUPZAUQURXMXQODZXQNDZUUGXMXPODZUEODZUUJXMUUHAODUULUUIAUSAUTVAUU
    MXMEOUEVHVBVCVDXPUEVEMZXQVFXQVGVAZAXRVIMXJXKUUEXLXJYPVJDUUEAVKYPVLURUPXJXKX
    LVMZXJXKXLVNZYPBCVOVPXMYAXRYDKGZFGZYBXRYEKGZFGZKGZYCUUTUURKGZFGZYAUUTKGZYBU
    URKGZFGZFGZYTYMXMYANDZUURNDZYBNDZUUTNDZUVBUVHLXMONYAVQXMPOYAVRXMXIEVSZPHWLZ
    XJXKYAPDUVNXMVTVDZUUBUUPABPXIEHQRSZSZXMUUGYDNDZUVJUUOXMONYDVQXMEOYDVHXMUVME
    JWLZXJXKYDEDUVSXMWAVDZUUBUUPABEXIEJQRSZSZXRYDWBMXMONYBVQXMPOYBVRXMUVNXJXLYB
    PDUVOUUBUUQACPXIEHQRSZSZXMUUGYENDZUVLUUOXMONYEVQXMEOYEVHXMUVSXJXLYEEDUVTUUB
    UUQACEXIEJQRSZSZXRYEWBMYAUURYBUUTWCVPXMUUSYRUVAYSKXMXJXKUUSYRLUUBUUPABUKMXM
    XJXLUVAYSLUUBUUQACUKMWDXMUVDYHUVGYLFXMUVCYGYCFXMUVCXRXRKGZYEYDKGZKGZYGXMUUG
    UWEUUGUVRUVCUWJLUUOUWGUUOUWBXRYEXRYDWEVPXMUWHXQUWIYFKXMXRUAIGZUWHXQXMUUGUWK
    UWHLUUOXRWFURXMXQVJDZUUKUWKXQLXJXKUWLXLAWGUPXQWHXQWIVAWJXMUWEUVRUWIYFLUWGUW
    BYEYDWMMWDWKWNXMUVGXRYJKGZXRYBYDKGZKGZFGZXRYJUWNFGZKGZYLXMUVEUWMUVFUWOFXMUV
    IUUGUWEUVEUWMLUVQUUOUWGYAXRYEWORXMUVKUUGUVRUVFUWOLUWDUUOUWBYBXRYDWORWDXMUUG
    YJNDZUWNNDZUWRUWPLUUOXMUVIUWEUWSUVQUWGYAYEWBMZXMUVKUVRUWTUWDUWBYBYDWBMZXRYJ
    UWNWPRXMUWQYKXRKXMUWQUWNYJFGZYKXMUWSUWTUWQUXCLUXAUXBYJUWNWQMXMUWNYIYJFXMUVK
    UVRUWNYILUWDUWBYBYDWMMWRWKWNWSWDWTXCXMXRNOXADZXOODXSODYHODZYKODZYNYOXDXJXKU
    XDXLAXBUPXMPOXOVRXMUVNXJUUAXOPDUVOUUBUUCAXNPXIEHQRSXMEOXSVHXMUVSXJUUAXSEDUV
    TUUBUUCAXNEXIEJQRSXMYCODZYGODZUXEXMYAODZYBODZUXGUVPUWCYAYBTMXMUUJYFODZUXHUU
    NXMYDODZYEODZUXKUWAUWFYDYETMXQYFTMYCYGXEMXMYIODZYJODZUXFXMUXLUXJUXNUWAUWCYD
    YBTMXMUXIUXMUXOUVPUWFYAYETMYIYJXEMXRXOXSYHYKXFXGXH $.
    $( [22-Sep-2014] $)

  $( Value of the X and Y sequences at 1.  (Contributed by Stefan O'Rear,
     22-Sep-2014.) $)
  rmxy1 $p |- ( A e. ( ZZ>= ` 2 ) -> ( ( A rmX 1 ) = A /\ ( A rmY 1 ) = 1 ) )
      $=
    ( c2 cfv wcel c1 crmx co cexp crmy cmul caddc wceq cz 1z mpan2 crp rpcn cn0
    cc cq cmin csqr wa rmxyval rmbaserp exp1 3syl rmspecpos sqrcl mulid1 eqcomd
    cuz oveq2d 3eqtrd cdif wb rmspecsqrnq nn0ssq frmx fovcl sseldi zssq eluzelz
    syl frmy zq sselii a1i qirropth syl122anc mpbid ) ABULCZDZAEFGZABHGEUAGZUBC
    ZAEIGZJGKGZAVPEJGZKGZLZVNALVQELUCZVMVRAVPKGZEHGZWCVTVMEMDZVRWDLNAEUDOVMWCPD
    WCSDWDWCLAUEWCQWCUFUGVMVPVSAKVMVSVPVMVPSDZVSVPLVMVOPDVOSDWFAUHVOQVOUIUGVPUJ
    VDUKUMUNVMVPSTUODVNTDVQTDATDZETDZWAWBUPAUQVMRTVNURVMWEVNRDNAERVLMFUSUTOVAVM
    MTVQVBVMWEVQMDNAEMVLMIVEUTOVAVMAMDWGBAVCAVFVDWHVMMTEVBNVGVHVPVNVQAEVIVJVK
    $.
    $( [22-Sep-2014] $)

  $( Value of the X and Y sequences at 0.  (Contributed by Stefan O'Rear,
     22-Sep-2014.) $)
  rmxy0 $p |- ( A e. ( ZZ>= ` 2 ) -> ( ( A rmX 0 ) = 1 /\ ( A rmY 0 ) = 0 ) )
      $=
    ( c2 cfv wcel cc0 crmx co cexp c1 crmy cmul caddc wceq cz 0z mpan2 cn0 zssq
    cc cq cuz cmin csqr wa rmxyval crp rmbaserp rpcn exp0 rmspecpos sqrcl mul01
    3syl syl oveq2d ax-1cn addid1i syl6req 3eqtrd cdif rmspecsqrnq nn0ssq fovcl
    wb frmx sseldi frmy 1z sselii a1i qirropth syl122anc mpbid ) ABUACZDZAEFGZA
    BHGIUBGZUCCZAEJGZKGLGZIVREKGZLGZMZVPIMVSEMUDZVOVTAVRLGZEHGZIWBVOENDZVTWFMOA
    EUEPVOWEUFDWESDWFIMAUGWEUHWEUIUMVOWBIELGIVOWAEILVOVRSDZWAEMVOVQUFDVQSDWHAUJ
    VQUHVQUKUMVRULUNUOIUPUQURUSVOVRSTUTDVPTDVSTDITDZETDZWCWDVDAVAVOQTVPVBVOWGVP
    QDOAEQVNNFVEVCPVFVONTVSRVOWGVSNDOAENVNNJVGVCPVFWIVONTIRVHVIVJWJVONTEROVIVJV
    RVPVSIEVKVLVM $.
    $( [22-Sep-2014] $)

  $( Negation law (even function) for the X sequence.  The method of proof used
     for the previous four theorems ~ rmxyneg , ~ rmxyadd , ~ rmxy0 , and
     ~ rmxy1 via ~ qirropth results in two theorems at once, but typical use
     requires only one, so this group of theorems serves to separate the
     cases.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmxneg $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmX -u N ) = ( A rmX N
      ) ) $=
    ( c2 cuz cfv wcel cz wa cneg crmx co wceq crmy rmxyneg simpld ) ACDEFBGFHAB
    IZJKABJKLAPMKABMKILABNO $.
    $( [22-Sep-2014] $)

  $( Value of X sequence at 0.  Part 1 of equation 2.11 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmx0 $p |- ( A e. ( ZZ>= ` 2 ) -> ( A rmX 0 ) = 1 ) $=
    ( c2 cuz cfv wcel cc0 crmx co c1 wceq crmy rmxy0 simpld ) ABCDEAFGHIJAFKHFJ
    ALM $.
    $( [22-Sep-2014] $)

  $( Value of X sequence at 1.  Part 2 of equation 2.11 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmx1 $p |- ( A e. ( ZZ>= ` 2 ) -> ( A rmX 1 ) = A ) $=
    ( c2 cuz cfv wcel c1 crmx co wceq crmy rmxy1 simpld ) ABCDEAFGHAIAFJHFIAKL
    $.
    $( [22-Sep-2014] $)

  $( Addition formula for X sequence.  Equation 2.7 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmxadd $p |- ( ( A e. ( ZZ>= ` 2 ) /\ M e. ZZ /\ N e. ZZ ) ->
        ( A rmX ( M + N ) ) = ( ( ( A rmX M ) x. ( A rmX N ) ) + ( ( ( A ^ 2 )
      - 1 ) x. ( ( A rmY M ) x. ( A rmY N ) ) ) ) ) $=
    ( c2 cuz cfv wcel cz w3a caddc crmx cmul cexp cmin crmy wceq rmxyadd simpld
    co c1 ) ADEFGBHGCHGIABCJSZKSABKSZACKSZLSADMSTNSABOSZACOSZLSLSJSPAUAOSUDUCLS
    UBUELSJSPABCQR $.
    $( [22-Sep-2014] $)

  $( Negation formula for Y sequence (odd function).  (Contributed by Stefan
     O'Rear, 22-Sep-2014.) $)
  rmyneg $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmY -u N ) = -u ( A
      rmY N ) ) $=
    ( c2 cuz cfv wcel cz wa cneg crmx co wceq crmy rmxyneg simprd ) ACDEFBGFHAB
    IZJKABJKLAPMKABMKILABNO $.
    $( [22-Sep-2014] $)

  $( Value of Y sequence at 0.  Part 1 of equation 2.12 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmy0 $p |- ( A e. ( ZZ>= ` 2 ) -> ( A rmY 0 ) = 0 ) $=
    ( c2 cuz cfv wcel cc0 crmx co c1 wceq crmy rmxy0 simprd ) ABCDEAFGHIJAFKHFJ
    ALM $.
    $( [22-Sep-2014] $)

  $( Value of Y sequence at 1.  Part 2 of equation 2.12 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmy1 $p |- ( A e. ( ZZ>= ` 2 ) -> ( A rmY 1 ) = 1 ) $=
    ( c2 cuz cfv wcel c1 crmx co wceq crmy rmxy1 simprd ) ABCDEAFGHAIAFJHFIAKL
    $.
    $( [22-Sep-2014] $)

  $( Addition formula for Y sequence.  Equation 2.8 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 22-Sep-2014.) $)
  rmyadd $p |- ( ( A e. ( ZZ>= ` 2 ) /\ M e. ZZ /\ N e. ZZ ) ->
        ( A rmY ( M + N ) ) = ( ( ( A rmY M ) x. ( A rmX N ) ) + ( ( A rmX M )
      x. ( A rmY N ) ) ) ) $=
    ( c2 cuz cfv wcel cz w3a caddc crmx cmul cexp cmin crmy wceq rmxyadd simprd
    co c1 ) ADEFGBHGCHGIABCJSZKSABKSZACKSZLSADMSTNSABOSZACOSZLSLSJSPAUAOSUDUCLS
    UBUELSJSPABCQR $.
    $( [22-Sep-2014] $)

  $( Special addition-of-1 formula for X sequence.  Part 1 of equation 2.9 of
     [JonesMatijasevic] p. 695.  (Contributed by Stefan O'Rear,
     19-Oct-2014.) $)
  rmxp1 $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) ->
        ( A rmX ( N + 1 ) ) = ( ( ( A rmX N ) x. A ) + ( ( ( A ^ 2 ) - 1 ) x. (
      A rmY N ) ) ) ) $=
    ( c2 cuz wcel cz wa c1 caddc co crmx cmul cexp cmin crmy wceq adantr oveq2d
    cfv eqtrd 1z rmxadd mp3an3 rmx1 rmy1 cc frmy fovcl zcn mulid1 3syl oveq12d
    ) ACDSZEZBFEZGZABHIJKJZABKJZAHKJZLJZACMJHNJZABOJZAHOJZLJZLJZIJZURALJZVAVBLJ
    ZIJUNUOHFEUQVFPUAABHUBUCUPUTVGVEVHIUPUSAURLUNUSAPUOAUDQRUPVDVBVALUPVDVBHLJZ
    VBUNVDVIPUOUNVCHVBLAUERQUPVBFEVBUFEVIVBPABFUMFOUGUHVBUIVBUJUKTRULT $.
    $( [19-Oct-2014] $)

  $( Special addition of 1 formula for Y sequence.  Part 2 of equation 2.9 of
     [JonesMatijasevic] p. 695.  (Contributed by Stefan O'Rear,
     24-Sep-2014.) $)
  rmyp1 $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) ->
        ( A rmY ( N + 1 ) ) = ( ( ( A rmY N ) x. A ) + ( A rmX N ) ) ) $=
    ( c2 cuz cfv wcel cz wa c1 caddc co crmy crmx cmul wceq oveq2d adantr eqtrd
    1z cn0 rmyadd mp3an3 rmx1 rmy1 cc frmx fovcl nn0cn mulid1 3syl oveq12d ) AC
    DEZFZBGFZHZABIJKLKZABLKZAIMKZNKZABMKZAILKZNKZJKZUQANKZUTJKUMUNIGFUPVCOSABIU
    AUBUOUSVDVBUTJUMUSVDOUNUMURAUQNAUCPQUOVBUTINKZUTUMVBVEOUNUMVAIUTNAUDPQUOUTT
    FUTUEFVEUTOABTULGMUFUGUTUHUTUIUJRUKR $.
    $( [24-Sep-2014] $)

  $( Subtraction of 1 formula for X sequence.  Part 1 of equation 2.10 of
     [JonesMatijasevic] p. 695.  (Contributed by Stefan O'Rear,
     14-Oct-2014.) $)
  rmxm1 $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) ->
        ( A rmX ( N - 1 ) ) = ( ( A x. ( A rmX N ) ) -
        ( ( ( A ^ 2 ) - 1 ) x. ( A rmY N ) ) ) ) $=
    ( c2 wcel cz c1 cneg caddc co crmx cmul cmin crmy wceq eqtrd adantr syl2anc
    oveq2d cc cn cuz cfv wa cexp 1nn0 nn0negzi rmxadd mp3an3 1z rmxneg rmx1 cn0
    mpan2 nn0sscn frmx fovcl sseldi eluzelz zcn mulcom rmyneg rmy1 negeqd zsscn
    frmy ax-1cn mulneg2 sylancl mulid1 3eqtrd csquarenn cdif rmspecnonsq eldifi
    syl nncn 3syl oveq12d adantl negsub mulcl 3eqtr3d ) ACUAUBZDZBEDZUCZABFGZHI
    ZJIZAABJIZKIZACUDIFLIZABMIZKIZGZHIZABFLIZJIWKWNLIZWFWIWJAWGJIZKIZWLWMAWGMIZ
    KIZKIZHIZWPWDWEWGEDWIXDNFUEUFABWGUGUHWFWTWKXCWOHWFWTWJAKIZWKWFWSAWJKWDWSANW
    EWDWSAFJIZAWDFEDZWSXFNUIAFUJUMAUKOPRWFWJSDZASDZXEWKNWFULSWJUNABULWCEJUOUPUQ
    ZWDXIWEWDAEDXICAURAUSVOPZWJAUTQOWFXCWLWMGZKIZWOWFXBXLWLKWFXBWMWGKIZWMFKIZGZ
    XLWDXBXNNWEWDXAWGWMKWDXAAFMIZGZWGWDXGXAXRNUIAFVAUMWDXQFAVBVCORPWFWMSDZFSDZX
    NXPNWFESWMVDABEWCEMVEUPUQZVFWMFVGVHWFXOWMWFXSXOWMNYAWMVIVOVCVJRWFWLSDZXSXMW
    ONWDYBWEWDWLTVKVLDWLTDYBAVMWLTVKVNWLVPVQPZYAWLWMVGQOVROWFWHWQAJWFBSDZXTWHWQ
    NWEYDWDBUSVSVFBFVTVHRWFWKSDZWNSDZWPWRNWFXIXHYEXKXJAWJWAQWFYBXSYFYCYAWLWMWAQ
    WKWNVTQWB $.
    $( [14-Oct-2014] $)

  $( Subtraction of 1 formula for Y sequence.  Part 2 of equation 2.10 of
     [JonesMatijasevic] p. 695.  (Contributed by Stefan O'Rear,
     19-Oct-2014.) $)
  rmym1 $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) ->
        ( A rmY ( N - 1 ) ) = ( ( ( A rmY N ) x. A ) - ( A rmX N ) ) ) $=
    ( c2 wcel cz c1 cmin co crmy cneg caddc crmx cmul wceq ax-1cn negsub oveq2d
    cc eqtrd adantr cuz cfv wa zcn adantl sylancl eqcomd nn0negzi rmyadd mp3an3
    1nn0 1z rmxneg mpan2 rmx1 rmyneg rmy1 negeqd cn0 nn0sscn frmx sseldi negcli
    fovcl mulcom mulm1 3eqtrd oveq12d zsscn frmy eluzelre recnd mulcl syl2anc
    syl ) ACUAUBZDZBEDZUCZABFGHZIHABFJZKHZIHZABIHZAWALHZMHZABLHZAWAIHZMHZKHZWDA
    MHZWGGHZVSVTWBAIVSWBVTVSBRDZFRDWBVTNVRWMVQBUDUEOBFPUFUGQVQVRWAEDWCWJNFUKUHA
    BWAUIUJVSWJWKWGJZKHZWLVSWFWKWIWNKVSWEAWDMVQWEANVRVQWEAFLHZAVQFEDZWEWPNULAFU
    MUNAUOSTQVSWIWGWAMHZWAWGMHZWNVSWHWAWGMVQWHWANVRVQWHAFIHZJZWAVQWQWHXANULAFUP
    UNVQWTFAUQURSTQVSWGRDZWARDWRWSNVSUSRWGUTABUSVPELVAVDVBZFOVCWGWAVEUFVSXBWSWN
    NXCWGVFVOVGVHVSWKRDZXBWOWLNVSWDRDARDZXDVSERWDVIABEVPEIVJVDVBVQXEVRVQACAVKVL
    TWDAVMVNXCWKWGPVNSVG $.
    $( [19-Oct-2014] $)

  $( The X sequence is a Lucas (second-order integer recurrence) sequence.
     Part 3 of equation 2.11 of [JonesMatijasevic] p. 695.  (Contributed by
     Stefan O'Rear, 14-Oct-2014.) $)
  rmxluc $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmX ( N + 1 ) ) =
      ( ( ( 2 x. A ) x. ( A rmX N ) ) - ( A rmX ( N - 1 ) ) ) ) $=
    ( c2 wcel cz wa cmul co crmx c1 cmin caddc wceq cc cn0 nn0sscn fovcl sseldi
    syl2anc mulcl cuz cexp crmy peano2zm frmx sylan2 peano2z addcom rmxp1 rmxm1
    cfv oveq12d eluzelz zcn syl adantr cn csquarenn cdif rmspecnonsq nncn zsscn
    eldifi 3syl ppncan syl3anc mulcom oveq1d 2cn mulass 2times eqtr2d 3eqtrd wb
    frmy a1i sylancr subadd mpbird eqcomd ) ACUAUKZDZBEDZFZCAGHZABIHZGHZABJKHZI
    HZKHZABJLHZIHZWDWJWLMZWIWLLHZWGMZWDWNWLWILHZWFAGHZACUBHJKHZABUCHZGHZLHZAWFG
    HZWTKHZLHZWGWDWINDZWLNDZWNWPMWCWBWHEDZXEBUDWBXGFONWIPAWHOWAEIUEQRUFZWCWBWKE
    DZXFBUGWBXIFONWLPAWKOWAEIUEQRUFZWIWLUHSWDWLXAWIXCLABUIABUJULWDXDWQXBLHZXBXB
    LHZWGWDWQNDZWTNDZXBNDZXDXKMWDWFNDZANDZXMWDONWFPABOWAEIUEQRZWBXQWCWBAEDXQCAU
    MAUNUOUPZWFATSWDWRNDZWSNDXNWBXTWCWBWRUQURUSDWRUQDXTAUTWRUQURVCWRVAVDUPWDENW
    SVBABEWAEUCVOQRWRWSTSWDXQXPXOXSXRAWFTSZWQWTXBVEVFWDWQXBXBLWDXPXQWQXBMXRXSWF
    AVGSVHWDWGCXBGHZXLWDCNDZXQXPWGYBMYCWDVIVPXSXRCAWFVJVFWDXOYBXLMYAXBVKUOVLVMV
    MWDWGNDZXEXFWMWOVNWDWENDZXPYDWDYCXQYEVIXSCATVQXRWEWFTSXHXJWGWIWLVRVFVSVT $.
    $( [14-Oct-2014] $)

  $( The Y sequence is a Lucas sequence, definable via this second-order
     recurrence with ~ rmy0 and ~ rmy1 .  Part 3 of equation 2.12 of
     [JonesMatijasevic] p. 695.  JonesMatijasevic uses this theorem to redefine
     the X and Y sequences to have domain ` ( ZZ X. ZZ ) ` , which simplifies
     some later theorems.  It may shorten the derivation to use this as our
     initial definition.  Incidentally, the X sequence satisfies the exact same
     recurrence.  (Contributed by Stefan O'Rear, 1-Oct-2014.) $)
  rmyluc $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmY ( N + 1 ) ) = ( (
      2 x. ( ( A rmY N ) x. A ) ) - ( A rmY ( N - 1 ) ) ) ) $=
    ( c2 wcel cz c1 caddc co crmy cmin cmul wceq crmx cc zsscn frmy fovcl mulcl
    sseldi syl2anc cuz cfv wa rmyp1 rmym1 oveq12d eluzelre recnd adantr nn0sscn
    cn0 frmx ppncan syl3anc 2cn sylancr peano2zm sylan2 npcan 2times syl eqtr2d
    3eqtrd wb peano2z subcl addcan2 mpbid ) ACUAUBZDZBEDZUCZABFGHZIHZABFJHZIHZG
    HZCABIHZAKHZKHZVPJHZVPGHZLZVNWALZVLVQVSABMHZGHZVSWEJHZGHZVSVSGHZWBVLVNWFVPW
    GGABUDABUEUFVLVSNDZWENDWJWHWILVLVRNDANDZWJVLENVROABEVIEIPQSVJWKVKVJACAUGUHU
    IVRARTZVLUKNWEUJABUKVIEMULQSWLVSWEVSUMUNVLWBVTWIVLVTNDZVPNDZWBVTLVLCNDWJWMU
    OWLCVSRUPZVLENVPOVKVJVOEDVPEDBUQAVOEVIEIPQURSZVTVPUSTVLWJVTWILWLVSUTVAVBVCV
    LVNNDWANDZWNWCWDVDVLENVNOVKVJVMEDVNEDBVEAVMEVIEIPQURSVLWMWNWQWOWPVTVPVFTWPV
    NWAVPVGUNVH $.
    $( [1-Oct-2014] $)

  $( Lucas sequence property of Y with better output ordering.  (Contributed by
     Stefan O'Rear, 16-Oct-2014.) $)
  rmyluc2 $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmY ( N + 1 ) ) =
        ( ( ( 2 x. A ) x. ( A rmY N ) ) - ( A rmY ( N - 1 ) ) ) ) $=
    ( c2 cuz cfv wcel cz wa c1 caddc co crmy cmul cmin rmyluc wceq frmy zcn syl
    cc fovcl eluzelz adantr mulcom syl2anc oveq2d 2cn a1i mulass syl3anc eqtr4d
    oveq1d eqtrd ) ACDEZFZBGFZHZABIJKLKCABLKZAMKZMKZABINKLKZNKCAMKURMKZVANKABOU
    QUTVBVANUQUTCAURMKZMKZVBUQUSVCCMUQURTFZATFZUSVCPUQURGFVEABGUNGLQUAURRSZUOVF
    UPUOAGFVFCAUBARSUCZURAUDUEUFUQCTFZVFVEVBVDPVIUQUGUHVHVGCAURUIUJUKULUM $.
    $( [16-Oct-2014] $)

  $( "Double-angle formula" for X-values.  Equation 2.13 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 2-Oct-2014.) $)
  rmxdbl $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmX ( 2 x. N ) ) = ( (
      2 x. ( ( A rmX N ) ^ 2 ) ) - 1 ) ) $=
    ( c2 wcel cz cmul co crmx caddc cexp c1 cmin crmy cc wceq 2times syl oveq2d
    cn0 cn cuz cfv wa zcn adantl rmxadd 3anidm23 nn0sscn frmx fovcl sseldi sqcl
    csquarenn cdif rmspecnonsq eldifi nncn 3syl adantr zsscn frmy mulcl syl2anc
    pnncan syl3anc eqcomd rmxynorm oveq12d sqval 3eqtr3rd 3eqtrd ) ACUAUBZDZBED
    ZUCZACBFGZHGABBIGZHGZABHGZVSFGZACJGKLGZABMGZWBFGZFGZIGZCVSCJGZFGZKLGZVOVPVQ
    AHVOBNDZVPVQOVNWIVMBUDUEBPQRVMVNVRWEOABBUFUGVOWFWFIGZWFWAWBCJGZFGZLGZLGZWFW
    LIGZWHWEVOWFNDZWPWLNDZWNWOOVOVSNDZWPVOSNVSUHABSVLEHUIUJUKZVSULQZWTVOWANDZWK
    NDZWQVMXAVNVMWATUMUNDWATDXAAUOWATUMUPWAUQURUSVOWBNDZXBVOENWBUTABEVLEMVAUJUK
    ZWBULQWAWKVBVCWFWFWLVDVEVOWJWGWMKLVOWGWJVOWPWGWJOWTWFPQVFABVGVHVOWFVTWLWDIV
    OWRWFVTOWSVSVIQVOWKWCWAFVOXCWKWCOXDWBVIQRVHVJVK $.
    $( [2-Oct-2014] $)

  $( "Double-angle formula" for Y-values.  Equation 2.14 of [JonesMatijasevic]
     p. 695.  (Contributed by Stefan O'Rear, 2-Oct-2014.) $)
  rmydbl $p |- ( ( A e. ( ZZ>= ` 2 ) /\ N e. ZZ ) -> ( A rmY ( 2 x. N ) ) = ( (
      2 x. ( A rmX N ) ) x. ( A rmY N ) ) ) $=
    ( c2 cuz cfv wcel cz cmul co crmy caddc crmx cc 2times syl cn0 fovcl sseldi
    wceq syl2anc wa zcn adantl oveq2d rmyadd 3anidm23 2cn a1i nn0sscn frmx frmy
    zsscn mulass syl3anc mulcl mulcom oveq1d 3eqtrrd 3eqtrd ) ACDEZFZBGFZUAZACB
    HIZJIABBKIZJIZABJIZABLIZHIZVHVGHIZKIZCVHHIVGHIZVCVDVEAJVCBMFZVDVESVBVMVABUB
    UCBNOUDVAVBVFVKSABBUEUFVCVLCVJHIZVJVJKIZVKVCCMFZVHMFZVGMFZVLVNSVPVCUGUHVCPM
    VHUIABPUTGLUJQRZVCGMVGULABGUTGJUKQRZCVHVGUMUNVCVJMFZVNVOSVCVQVRWAVSVTVHVGUO
    TVJNOVCVJVIVJKVCVQVRVJVISVSVTVHVGUPTUQURUS $.
    $( [2-Oct-2014] $)

