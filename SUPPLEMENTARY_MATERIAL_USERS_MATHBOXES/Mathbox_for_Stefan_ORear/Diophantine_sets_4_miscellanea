$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Diophantine sets 4 miscellanea
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A c $.  $d B c $.  $d C b $.  $d a c $.  $d b c $.  $d C a $.
    2sbcrex.1 $e |- A e. _V $.
    2sbcrex.2 $e |- B e. _V $.
    $( Exchange an existential quantifier with two substitutions.  (Contributed
       by Stefan O'Rear, 11-Oct-2014.) $)
    2sbcrex $p |- ( [ A / a ] [ B / b ] E. c e. C ph <-> E. c e. C [ A / a ] [
        B / b ] ph ) $=
      ( wrex wsbc cvv wcel wb sbcrexg ax-mp sbcbii bitri ) AGDJFCKZEBKZAFCKZGDJ
      ZEBKZUAEBKGDJZBLMZTUCNHSUBEBLCLMSUBNIAFGCDLOPQPUEUCUDNHUAEGBDLOPR $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d A b $.  $d A c $.  $d B a $.  $d C a $.  $d a b $.  $d a c $.
    $( Exchange a substitution with two existentials.  (Contributed by Stefan
       O'Rear, 11-Oct-2014.) $)
    sbc2rexg $p |- ( A e. V -> ( [ A / a ] E. b e. B E. c e. C ph <-> E. b e. B
        E. c e. C [ A / a ] ph ) ) $=
      ( wcel cvv wrex wsbc wb elex sbcrexg rexbidv bitrd syl ) BEIBJIZAHDKZGCKF
      BLZAFBLHDKZGCKZMBENSUATFBLZGCKUCTFGBCJOSUDUBGCAFHBDJOPQR $.
      $( [11-Oct-2014] $)

    $d A d $.  $d A e $.  $d D a $.  $d E a $.  $d a d $.  $d a e $.
    $( Exchange a substitution with 4 existentials.  (Contributed by Stefan
       O'Rear, 11-Oct-2014.) $)
    sbc4rexg $p |- ( A e. V -> ( [ A / a ] E. b e. B E. c e. C E. d e. D E. e
        e. E ph <-> E. b e. B E. c e. C E. d e. D E. e e. E [ A / a ] ph ) ) $=
      ( wcel cvv wrex wsbc wb elex sbc2rexg 2rexbidv bitrd syl ) BHMBNMZAFGOLEO
      ZKDOJCOIBPZAIBPFGOLEOZKDOJCOZQBHRUCUEUDIBPZKDOJCOUGUDBCDNIJKSUCUHUFJKCDAB
      EGNILFSTUAUB $.
      $( [11-Oct-2014] $)
  $}

  ${
    sbcbiii.1 $e |- A e. _V $.
    sbcbiii.2 $e |- ( ph <-> ps ) $.
    $( Fully inferenced rewriting under an explicit substitution.  (Contributed
       by Stefan O'Rear, 11-Oct-2014.) $)
    sbcbiii $p |- ( [ A / a ] ph <-> [ A / a ] ps ) $=
      ( cvv wcel wsbc wb sbcbii ax-mp ) CGHADCIBDCIJEABDCGFKL $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d A b $.  $d A c $.  $d B a $.  $d C a $.  $d a c $.  $d a b $.
    $( also my first direct use of ax-4 $)
    $( Rotate a sequence of three explicit substitutions, closed theorem.
       (Contributed by Stefan O'Rear, 11-Oct-2014.) $)
    sbcrot3g $p |- ( ( A e. D /\ B e. E /\ A. b C e. F ) -> ( [ A / a ] [ B / b
        ] [ C / c ] ph <-> [ B / b ] [ C / c ] [ A / a ] ph ) ) $=
      ( wcel cvv wal wsbc wb elex alimi w3a sbccomg 3adant3 wa simp2 ax-17 hba1
      3simpb hban ax-4 sylan2 sbcbidg syl2anc bitrd syl3an ) BEKBLKZCFKCLKZDGKZ
      IMDLKZIMZAJDNZICNHBNZAHBNJDNZICNZOBEPCFPUOUPIDGPQUMUNUQRZUSURHBNZICNZVAUM
      UNUSVDOUQURHIBCLLSTVBUMUQUAZUNVDVAOUMUNUQUEUMUNUQUBVEVCUTICLUMUQIUMIUCUPI
      UDUFUQUMUPVCUTOUPIUGAHJBDLLSUHUIUJUKUL $.
      $( [11-Oct-2014] $)

    sbcrot3.1 $e |- A e. _V $.
    sbcrot3.2 $e |- B e. _V $.
    sbcrot3.3 $e |- C e. _V $.
    $( Rotate a sequence of three explicit substitutions.  Substituted values
       must be manifest sets.  (Contributed by Stefan O'Rear, 11-Oct-2014.) $)
    sbcrot3 $p |- ( [ A / a ] [ B / b ] [ C / c ] ph <-> [ B / b ] [ C / c ] [
        A / a ] ph ) $=
      ( cvv wcel wal wsbc wb ax-gen sbcrot3g mp3an ) BKLCKLDKLZFMAGDNFCNEBNAEBN
      GDNFCNOHISFJPABCDKKKEFGQR $.
      $( [11-Oct-2014] $)

    $d A d $.  $d A e $.  $d D a $.  $d E a $.  $d a e $.  $d a d $.
    sbcrot5.4 $e |- D e. _V $.
    sbcrot5.5 $e |- E e. _V $.
    $( Rotate a sequence of five explicit substitutions.  Substituted values
       must be manifest sets.  (Contributed by Stefan O'Rear, 11-Oct-2014.) $)
    sbcrot5 $p |- ( [ A / a ] [ B / b ] [ C / c ] [ D / d ] [ E / e ] ph <-> [
        B / b ] [ C / c ] [ D / d ] [ E / e ] [ A / a ] ph ) $=
      ( wsbc sbcrot3 sbcbiii bitri ) AFGQKEQZJDQICQHBQUAHBQZJDQZICQAHBQFGQKEQZJ
      DQZICQUABCDHIJLMNRUCUECIMUBUDDJNABEGHKFLOPRSST $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d A a c $.  $d A a b $.  $d C a c $.
    sbccomieg.1 $e |- ( a = A -> B = C ) $.
    $( Commute two explicit substitutions, using an implicit substitution to
       rewrite the exiting substitution.  (Contributed by Stefan O'Rear,
       11-Oct-2014.) $)
    sbccomieg $p |- ( ( A e. V /\ C e. W ) -> ( [ A / a ] [ B / b ] ph <-> [ C
        / b ] [ A / a ] ph ) ) $=
      ( vc wcel wa cvv wsbc wal wi cv wceq wb ax-17 elex adantr hbsbc1g hbsbcgd
      adantl a17d alrimiv syl2anc dfsbcq sbceq1a sbcbidgv ancoms bitrd sbciegft
      syl ex syl3anc ) BEKZDFKZLZBMKZAGBNZHDNZVCGOPZGOZGQBRZAHCNZVCSZPZGOZVGGBN
      VCSURVAUSBEUAUBZUTVADMKZVEVKUSVLURDFUAUEZVAVLLVDGVAVBGHJDMVAGTVAHTVAJQZDK
      GUFAGJBMVNBKGTUCUDUGUHUTVLVJVMVLVIGVLVFVHVLVFLZVGAHDNZVCVOCDRZVGVPSVFVQVL
      IUEAHCDUIUOVFVLVPVCSVFAVBHDMAGBUJUKULUMUPUGUOVGVCGBMUNUQ $.
      $( [11-Oct-2014] $)
  $}

