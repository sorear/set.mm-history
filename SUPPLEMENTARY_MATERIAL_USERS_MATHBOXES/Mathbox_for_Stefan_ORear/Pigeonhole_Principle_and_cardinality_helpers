$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Pigeonhole Principle and cardinality helpers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y a b c d $.  $d B x y a b c d $.  $d C y a b c d $.
    $d D x a b c d $.  $d ph x y a b c d $.
    fphpd.a $e |- ( ph -> B ~< A ) $.
    fphpd.b $e |- ( ( ph /\ x e. A ) -> C e. B ) $.
    fphpd.c $e |- ( x = y -> C = D ) $.
    $( Pigeonhole principle expressed with implicit substitution.  If the range
       is smaller than the domain, two inputs must be mapped to the same
       output.  (Contributed by Stefan O'Rear, 19-Oct-2014.) $)
    fphpd $p |- ( ph -> E. x e. A E. y e. A ( x =/= y /\ C = D ) ) $=
      ( va vb wceq weq wi wral wn wa wcel ax-17 cv wne wrex cdom wbr csdm nsyl3
      domnsym cvv sdomex simprd syl adantr csb hbcsb1 hbel eleq1 anbi2d csbeq1a
      wel hbim eleq1d imbi12d chvar ex wb csbid vex csbief eqeq12i 2ralbii hbeq
      imbi1i csbeq1 eqeq1d eqeq1 eqeq2d eqeq2 rcla42 sylbir impbid1 syl6 adantl
      com12 id dom2d mpd mtand ancom anbi1i pm4.61 3bitr4i rexbii rexnal sylibr
      df-ne bitri ) AFGMZBCNZOZCDPZBDPZQZBUAZCUAZUBZWRRZCDUCZBDUCZAXBDEUDUEZXJE
      DUFUEZADEUHHUGAXBRZDUISZXJAXMXBAXKXMHXKEUISXMEDUJUKULUMXLKLDEBKUAZFUNZBLU
      AZFUNZUIAXNDSZXOESZOXBAXRXSAXDDSZRZFESZOAXRRZXSOBKYCXSBYCBTBLLXOEBLXNFLKU
      TBTUOZXPESBTUPVABKNZYAYCYBXSYEXTXRAXDXNDUQURYEFXOEBXNFUSVBVCIVDVEUMXBXRXP
      DSRZXOXQMZKLNZVFZOAXBYFYGYHOZYIXBBXDFUNZBXEFUNZMZWSOZCDPBDPZYFYJOYNWTBCDD
      YMWRWSYKFYLGBFVGBKXEFGCVHXNGSBTJVIVJVMVKYFYOYJYNYJXOYLMZKCNZOBCXNXPDDYPYQ
      BBLLXOYLYDBLXEFLCUTBTUOVLYQBTVAYJCTYEYMYPWSYQYEYKXOYLBXDXNFVNVOXDXNXEVPVC
      CLNZYPYGYQYHYRYLXQXOBXEXPFVNVQXEXPXNVRVCVSWDVTYJYGYHYJWEBXNXPFVNWAWBWCWFW
      GWHXIXAQZBDUCXCXHYSBDXHWTQZCDUCYSXGYTCDWSQZWRRWRUUARXGYTUUAWRWIXFUUAWRXDX
      EWPWJWRWSWKWLWMWTCDWNWQWMXABDWNWQWO $.
      $( [19-Oct-2014] $)
  $}

  ${
    $d ph x y z a b c $.  $d A x y z a b c $.  $d B z a b c $.
    $d C x y a b c $.  $d D y z a b c $.  $d E x z a b c $.
    fphpdo.1 $e |- ( ph -> A C_ RR ) $.
    fphpdo.2 $e |- ( ph -> B e. _V ) $.
    fphpdo.3 $e |- ( ph -> B ~< A ) $.
    fphpdo.4 $e |- ( ( ph /\ z e. A ) -> C e. B ) $.
    fphpdo.5 $e |- ( z = x -> C = D ) $.
    fphpdo.6 $e |- ( z = y -> C = E ) $.
    $( Pigeonhole principle for sets of real numbers with implicit output
       reordering.  (Contributed by Stefan O'Rear, 12-Sep-2014.) $)
    fphpdo $p |- ( ph -> E. x e. A E. y e. A ( x < y /\ D = E ) ) $=
      ( vb wceq wa clt wcel vc cv wne cmpt cfv wrex wf eqid fmptd ffvelrn sylan
      wbr fveq2 fphpd wo cr sselda adantrr adantr adantrl lttri2 syl2anc simprl
      wb ad2antrr simprr simpr simplr weq breq1 eqeq1d anbi12d eqeq2d syl112anc
      breq2 rcla42ev ex eqcomd jaod anbi2d eleq1d imbi12d chvarv fvmptg adantlr
      wi eleq1 eqeq12d biimpd anim2d reximdva sylbid expimpd ancomsd rexlimdvva
      syld mpd ) APUBZUAUBZUCZWRDEGUDZUEZWSXAUEZQZRZUAEUFPEUFBUBZCUBZSULZHIQZRZ
      CEUFZBEUFZAPUAEFXBXCLAEFXAUGWRETZXBFTADEGFXAMXAUHZUIEFWRXAUJUKWRWSXAUMUNA
      XEXLPUAEEAXMWSETZRZRZXDWTXLXQXDWTXLXQXDRZWTWRWSSULZWSWRSULZUOZXLXRWRUPTZW
      SUPTZWTYAVDXQYBXDAXMYBXOAEUPWRJUQURUSXQYCXDAXOYCXMAEUPWSJUQUTUSWRWSVAVBXR
      YAXHXFXAUEZXGXAUEZQZRZCEUFZBEUFZXLXRXSYIXTXRXSYIXRXSRXMXOXSXDYIXQXMXDXSAX
      MXOVCZVEXQXOXDXSAXMXOVFZVEXRXSVGXQXDXSVHYGXSXDRWRXGSULZXBYEQZRBCWRWSEEBPV
      IZXHYLYFYMXFWRXGSVJYNYDXBYEXFWRXAUMVKVLCUAVIZYLXSYMXDXGWSWRSVOYOYEXCXBXGW
      SXAUMVMVLVPVNVQXRXTYIXRXTRZXOXMXTXCXBQZYIXQXOXDXTYKVEXQXMXDXTYJVEXRXTVGYP
      XBXCXQXDXTVHVRYGXTYQRWSXGSULZXCYEQZRBCWSWREEBUAVIZXHYRYFYSXFWSXGSVJYTYDXC
      YEXFWSXAUMVKVLCPVIZYRXTYSYQXGWRWSSVOUUAYEXBXCXGWRXAUMVMVLVPVNVQVSAYIXLWFX
      PXDAYHXKBEAXFETZRZYGXJCEUUCXGETZRZYFXIXHUUEYFXIUUEYDHYEIUUEUUBHFTZYDHQAUU
      BUUDVHUUCUUFUUDADUBZETZRZGFTZWFZUUCUUFWFDBDBVIZUUIUUCUUJUUFUULUUHUUBAUUGX
      FEWGVTUULGHFNWAWBMWCUSDXFGHEFXANXNWDVBUUEUUDIFTZYEIQUUCUUDVGAUUDUUMUUBUUK
      AUUDRZUUMWFDCDCVIZUUIUUNUUJUUMUUOUUHUUDAUUGXGEWGVTUUOGIFOWAWBMWCWEDXGGIEF
      XAOXNWDVBWHWIWJWKWKVEWPWLWMWNWOWQ $.
      $( [12-Sep-2014] $)
  $}

  $( An infinite subset of a countable set is countable, without using choice.
     (Contributed by Stefan O'Rear, 19-Oct-2014.) $)
  ctbnfien $p |- ( ( ( X ~~ om /\ Y ~~ om ) /\
        ( A C_ X /\ -. A e. Fin ) ) -> A ~~ Y ) $=
    ( com cen wbr wa wss cfn wcel wn csdm isfinite3 notbii wo cdom cvv wi relen
    syl2anc brrelexi ssdom2g syl simpl domentr brdom2 sylib adantlr syl5bi impr
    imp ord omex ensym ad2antlr entr ) BDEFZCDEFZGZABHZAIJZKZGZGADEFZDCEFZACEFU
    SUTVBVDVBADLFZKUSUTGZVDVAVFAMNVGVFVDUQUTVFVDOZURUQUTGZADPFZVHVIABPFZUQVJUQU
    TVKUQBQJUTVKRBDESUAABQUBUCUKUQUTUDABDUETADUFUGUHULUIUJURVEUQVCCDUMUNUOADCUP
    T $.
    $( [19-Oct-2014] $)

  ${
    $d A x y z $.  $d ph x y z $.  $d B x y z $.  $d D y z $.  $d E x $.
    fiphp3d.a $e |- ( ph -> A ~~ NN ) $.
    fiphp3d.b $e |- ( ph -> B e. Fin ) $.
    fiphp3d.c $e |- ( ( ph /\ x e. A ) -> D e. B ) $.
    $( Infinite pigeonhole principle for partitioning an infinite set between
       finitely many buckets.  (Contributed by Stefan O'Rear, 18-Oct-2014.) $)
    fiphp3d $p |- ( ph -> E. y e. B { x e. A | D = y } ~~ NN ) $=
      ( cv wceq crab cfn wcel wrex cn cen wbr com wa wn wral ominf risset eqcom
      ciun rexbii bitri sylib ralrimiva rabid2 sylibr iunrab syl6reqr eleq1d wb
      cvv omex nnenom entr sylancl enfi sylancr bitrd mtbiri iunfi sylan rexnal
      mtand wss jctir ssrab2 jctl ctbnfien syl2an ex reximdv mpd ) AFCJZKZBDLZM
      NZUAZCEOZWAPQRZCEOAWBCEUBZUAWDAWFCEWAUFZMNZAWHSMNZUCAWHDMNZWIAWGDMADVTCEO
      ZBDLZWGAWKBDUBDWLKAWKBDABJDNTFENZWKIWMVSFKZCEOWKCFEUDWNVTCEVSFUEUGUHUIUJW
      KBDUKULVTCBEDUMUNUOASUQNDSQRZWJWIUPURADPQRPSQRZWOGUSDPSUTVAZDSUQVBVCVDVEA
      EMNWFWHHCEWAVFVGVIWBCEVHULAWCWECEAWCWEAWOWPTWADVJZWCTWEWCAWOWPWQUSVKWCWRV
      TBDVLVMWADPVNVOVPVQVR $.
      $( [18-Oct-2014] $)
  $}

