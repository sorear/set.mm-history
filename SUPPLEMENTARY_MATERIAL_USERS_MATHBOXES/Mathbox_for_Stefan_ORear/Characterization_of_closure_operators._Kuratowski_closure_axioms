$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Characterization of closure operators.  Kuratowski closure axioms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopOn $.

  $( The class function of all topologies over a base set. $)
  ctopon $a class TopOn $.

  ${
    $d b j $.
    $( Define the set of topologies with a given base set. $)
    df-topon $a |- TopOn = ( b e. _V |-> { j e. Top | U. j = b } ) $.
  $}

  ${
    $d B b j $.  $d J b j $.
    $( Property of bieng a topology with a given base set.  (Contributed by
       Stefan O'Rear, 31-Jan-2015.) $)
    istopon $p |- ( J e. ( TopOn ` B ) <-> ( J e. Top /\ U. J = B ) ) $=
      ( vj vb ctopon cfv wcel cvv ctop cuni wceq wa cdm cv crab cpw vex wss a1i
      pwex elfvdm elex syl uniexg eleq1 syl5ibcom imp eqeq2 rabbidv df-topon wi
      rabss pwuni mpbir pweqd syl5eleq mprgbir ssexi fvmpt3 eleq2d unieq eqeq1d
      elpw pweq elrab syl6bb pm5.21nii ) BAEFZGZAHGZBIGZBJZAKZLZVIAEMZGVJBAEUAA
      VOUBUCVKVMVJVKVLHGVMVJBIUDVLAHUEUFUGVJVIBCNZJZAKZCIOZGVNVJVHVSBDAVQDNZKZC
      IOZVSHEHVTAKWAVRCIVTAVQUHUICDUJWBHGVTHGWBVTPZPZWCVTDQTTWBWDRWAVPWDGUKZCIW
      ACIWDULWEVPIGWAVPVQPZPZWDVPWGGVPWFRVPUMVPWFCQVCUNWAWFWCVQVTVDUOUPSUQURSUS
      UTVRVMCBIVPBKVQVLAVPBVAVBVEVFVG $.
      $( [31-Jan-2015] $)
  $}

  ${
    $d ph a b c x y z $.  $d M a b c x y z $.  $d J a b c x y $.
    $d B a b c x y z $.
    mretopd.m $e |- ( ph -> M e. ( Moore ` B ) ) $.
    mretopd.z $e |- ( ph -> (/) e. M ) $.
    mretopd.u $e |- ( ( ph /\ x e. M /\ y e. M ) -> ( x u. y ) e. M ) $.
    mretopd.j $e |- J = { z e. ~P B | ( B \ z ) e. M } $.
    $( A Moore collection which is closed under finite unions called
       _topological_; such a collection is the closed sets of a canonically
       associated topology.  (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    mretopd $p |- ( ph -> ( J e. ( TopOn ` B ) /\ M = ( Clsd ` J ) ) ) $=
      ( va vb wcel wceq wss c0 eleq1d cdif syl ctopon cfv ccld ctop cuni cv wal
      wi cin wral wa unieq uni0 syl6eq wne cpw crab ssrab2 eqsstri mpan2 adantl
      sstr sspwuni sylib vex uniex elpw sylibr adantr ciun difeq2i ciin iindif2
      uniiun cmre ad2antrr simpr weq difeq2 elrab2 simprbi rgen ssralv mreiincl
      mpi syl3anc eqeltrrd syl5eqel sylanbrc 0elpw mre1cl dif0 pm2.61ne alrimiv
      a1i ex inss1 simplbi elpwi ad2antrl syl5ss inex1 cun ad2antll simpl uneq1
      difindi imbi2d uneq2 3expb expcom vtocl2ga imp syl21anc ralrimivva cvv wb
      pwexg rabexg mpbir2and uniss ax-mp unipw sseqtri difidALT unissel sylancr
      istopg pwidg istopon eqid cldval pweqd difeq1d eleq2i difss elpw2g mpbiri
      rabeqbidv elrab3 syl5bb dfss4 bitrd incom dfin5 eqtri mresspw df-ss eqtrd
      rabbidva syl5eqr 3eqtrrd jca ) AFEUAUBNZGFUCUBZOAFUDNZFUEZEOZUUNAUUPLUFZF
      PZUUSUEZFNZUHZLUGZUUSMUFZUIZFNZMFUJLFUJZAUVCLAUUTUVBAUUTUKZUVBQFNZUUSQUUS
      QOZUVAQFUVKUVAQUEQUUSQULUMUNRUVIUUSQUOZUKZUVAEUPZNZEUVASZGNZUVBUVIUVOUVLU
      VIUVAEPZUVOUVIUUSUVNPZUVRUUTUVSAUUTFUVNPZUVSFEDUFZSZGNZDUVNUQZUVNKUWCDUVN
      URUSZUUSFUVNVBUTVAUUSEVCVDUVAEUUSLVEZVFVGVHVIUVMUVPEMUUSUVEVJZSZGUVAUWGEM
      UUSVNVKUVMMUUSEUVESZVLZUWHGUVLUWJUWHOUVIMUUSEUVEVMVAUVMGEVOUBNZUVLUWIGNZM
      UUSUJZUWJGNAUWKUUTUVLHVPUVIUVLVQUVIUWMUVLUVIUWLMFUJZUWMUWLMFUVEFNZUVEUVNN
      UWLUWCUWLDUVEUVNFDMVRUWBUWIGUWAUVEEVSRKVTWAZWBUUTUWNUWMUHAUWLMUUSFWCVAWEV
      IMGUWIUUSEWDWFWGWHUWCUVQDUVAUVNFUWAUVAOUWBUVPGUWAUVAEVSRKVTWIAUVJUUTAQUVN
      NZEGNZUVJUWQAEWJWOAUWKUWRHGEWKTZUWCUWRDQUVNFUWAQOZUWBEGUWTUWBEQSEUWAQEVSE
      WLUNRKVTWIVIWMWPWNAUVGLMFFAUUSFNZUWOUKZUKZUVFUVNNZEUVFSZGNZUVGUXCUVFEPUXD
      UXCUVFUUSEUUSUVEWQUXAUUSEPZAUWOUXAUUSUVNNZUXGUXAUXHEUUSSZGNZUWCUXJDUUSUVN
      FDLVRUWBUXIGUWAUUSEVSRKVTZWRUUSEWSTWTXAUVFEUUSUVEUWFXBVGVHUXCUXEUXIUWIXCZ
      GEUUSUVEXGUXCUXJUWLAUXLGNZUXAUXJAUWOUXAUXHUXJUXKWAWTUWOUWLAUXAUWPXDAUXBXE
      UXJUWLUKAUXMABUFZCUFZXCZGNZUHAUXIUXOXCZGNZUHAUXMUHBCUXIUWIGGUXNUXIOZUXQUX
      SAUXTUXPUXRGUXNUXIUXOXFRXHUXOUWIOZUXSUXMAUYAUXRUXLGUXOUWIUXIXIRXHAUXNGNZU
      XOGNZUKUXQAUYBUYCUXQJXJXKXLXMXNWHUWCUXFDUVFUVNFUWAUVFOUWBUXEGUWAUVFEVSRKV
      TWIXOAFXPNUUPUVDUVHUKXQAFUWDXPKAUVNXPNZUWDXPNAUWRUYDUWSEGXRTUWCDUVNXPXSTW
      HLMXPFYHTXTZAUUQEPEFNZUURUUQUVNUEZEUVTUUQUYGPUWEFUVNYAYBEYCYDAEUVNNZEESZG
      NZUYFAUWRUYHUWSEGYITAUYIQGEYEIWHUWCUYJDEUVNFUWAEOUWBUYIGUWAEEVSRKVTWIFEYF
      YGZEFYJWIAUUOUUQUXNSZFNZBUUQUPZUQZEUXNSZFNZBUVNUQZGAUUPUUOUYOOUYEBFUUQUUQ
      YKYLTAUYMUYQBUYNUVNAUUQEUYKYMAUYLUYPFAUUQEUXNUYKYNRYSAUYRUYBBUVNUQZGAUYQU
      YBBUVNAUXNUVNNZUKZUYQEUYPSZGNZUYBUYQUYPUWDNZVUAVUCFUWDUYPKYOAVUDVUCXQZUYT
      AUYPUVNNZVUEAVUFUYPEPZEUXNYPAUWRVUFVUGXQUWSUYPEGYQTYRUWCVUCDUYPUVNUWAUYPO
      UWBVUBGUWAUYPEVSRYTTVIUUAVUAVUBUXNGUYTVUBUXNOZAUYTUXNEPVUHUXNEWSUXNEUUBVD
      VARUUCUUJAUYSGUVNUIZGVUIUVNGUIUYSGUVNUUDBUVNGUUEUUFAGUVNPZVUIGOAUWKVUJHGE
      UUGTGUVNUUHVDUUKUUIUULUUM $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d B a b c x y $.  $d V a b c x y $.  $d K x y $.
    $( The topologies over a given base set form a Moore collection: the
       intersection of any family of them is a topology, including the empty
       (relative) intersection which gives the discrete topology ~ distop .
       (Contributed by Stefan O'Rear, 31-Jan-2015.) $)
    toponmre $p |- ( B e. V -> ( TopOn ` B ) e. ( Moore ` ~P B ) ) $=
      ( va vb vc vx vy cv wcel wceq wss cuni ctop istopon sylibr wa wral adantl
      syl wel ctopon cfv cpw cmre fveq2 pweq fveq2d eleq12d wtru simprbi eqimss
      sspwuni vex elpw ssriv a1i distop unipw mpbir2an c0 wne cint wi wal simpl
      cin sselda adantrl simplbi intss1 sstrd uniopn expr ralrimiv uniex elint2
      syl2anc ex alrimiv simpll simplrl sseldd simplrr inopn syl3anc ralrimivva
      ralrimiva inex1 cvv wb intex biimpi istopg mpbir2and wex ad2antlr elssuni
      n0 ancoms sseqtrd exlimdv mpd unissb eqid topopn eqeltrrd unissel 3adant1
      sylanbrc ismred trud vtoclg ) CHZUAUBZXMUCZUDUBZIZAUAUBZAUCZUDUBZICABXMAJ
      ZXNXRXPXTXMAUAUEYAXOXSUDXMAUFUGUHXQUIXNXODXNXOUCZKUIDXNYBDHZXNIZYCXOKZYCY
      BIYDYCLZXMJZYEYDYCMIYGXMYCNUJYGYFXMKYEYFXMUKYCXMULOSYCXODUMUNOUOUPXOXNIZU
      IYHXOMIXOLXMJXMCUMZUQXMURXMXONUSUPYCXNKZYCUTVAZYCVBZXNIZUIYJYKPZYLMIZYLLZ
      XMJZYMYNYOEHZYLKZYRLZYLIZVCZEVDZYRFHZVFZYLIZFYLQEYLQZYNUUBEYNYSUUAYNYSPZY
      TUUDIZFYCQUUAUUHUUIFYCYNYSFDTZUUIYNYSUUJPZPZUUDMIZYRUUDKZUUIUULUUDXNIZUUM
      YNUUJUUOYSYNYCXNUUDYJYKVEZVGZVHUUOUUMUUDLZXMJZXMUUDNZVISUUKUUNYNUUKYRYLUU
      DYSUUJVEUUJYLUUDKYSUUDYCVJZRVKRYRUUDVLVQVMVNFYTYCYREUMZVOVPOVRVSYNUUFEFYL
      YLYNYRYLIZUUDYLIZPZPZUUEGHZIZGYCQUUFUVFUVHGYCUVFGDTZPZUVGMIZEGTFGTUVHUVJU
      VGXNIZUVKUVFYCXNUVGYJYKUVEVTVGUVLUVKUVGLXMJXMUVGNVISUVJYLUVGYRUVIYLUVGKUV
      FUVGYCVJRZYNUVCUVDUVIWAWBUVJYLUVGUUDUVMYNUVCUVDUVIWCWBYRUUDUVGWDWEWGGUUEY
      CYRUUDUVBWHVPOWFYNYLWIIZYOUUCUUGPWJYKUVNYJYKUVNYCWKWLREFWIYLWMSWNYNYPXMKZ
      XMYLIZYQYNYRXMKZEYLQUVOYNUVQEYLYNUVCPZUUJFWOZUVQYKUVSYJUVCYKUVSFYCWRWLWPU
      VRUUJUVQFYNUVCUUJUVQYNUVCUUJPZPZYRUURXMUVTYRUURKZYNUVTEFTZUWBUUJUVCUWCUUJ
      YLUUDYRUVAVGWSYRUUDWQSRUWAUUOUUSYNUUJUUOUVCUUQVHUUOUUMUUSUUTUJSWTVMXAXBWG
      EYLXMXCOYNCETZEYCQUVPYNUWDEYCYNEDTPZYTXMYRUWEYRXNIZYTXMJZYNYCXNYRUUPVGZUW
      FYRMIZUWGXMYRNZUJSUWEUWIYTYRIUWEUWFUWIUWHUWFUWIUWGUWJVISYRYTYTXDXESXFWGEX
      MYCYIVPOYLXMXGVQXMYLNXIXHXJXKXL $.
      $( [31-Jan-2015] $)

    $( The closed sets of a topology over a set are a Moore collection over the
       same set.  (Contributed by Stefan O'Rear, 31-Jan-2015.) $)
    cldmreon $p |- ( J e. ( TopOn ` B ) -> ( Clsd ` J ) e. ( Moore ` B ) ) $=
      ( ctopon cfv wcel ccld cuni cmre ctop wceq istopon simplbi cldmre simprbi
      eqid syl fveq2d eleqtrd ) BACDEZBFDZBGZHDZAHDSBIEZTUBESUCUAAJZABKZLBUAUAO
      MPSUAAHSUCUDUENQR $.
      $( [31-Jan-2015] $)

    $d K a b c d $.
    $( A family is the closed sets of a topology iff it is a Moore collection
       and closed under finite union.  (Contributed by Stefan O'Rear,
       1-Feb-2015.) $)
    iscldtop $p |- ( K e. ( Clsd " ( TopOn ` B ) ) <-> ( K e. ( Moore ` B ) /\
          (/) e. K /\ A. x e. K A. y e. K ( x u. y ) e. K ) ) $=
      ( va vb vc ccld cfv wcel c0 cv cun wral w3a wceq ctop fncld ax-mp syl wfn
      cima cmre wrex wfun fnfun fvelima mpan cldmreon cuni istopon simplbi 0cld
      ctopon uncld 3expb sylan ralrimivva 3jca eleq1 eleq2 raleqbi1dv 3anbi123d
      wa syl5ibcom rexlimiv cdif cpw crab simp1 simp2 wi weq uneq1 eleq1d uneq2
      rcla42v com12 3ad2ant3 3impib eqid mretopd simprd eqcomd simpld cdm ssriv
      wss fndm sseqtr4i funfvima2 mp2an eqeltrrd impbii ) DHCUNIZUBZJZDCUCIZJZK
      DJZALZBLZMZDJZBDNZADNZOZWQELZHIZDPZEWOUDZXGHUEZWQXKHQUAZXLRQHUFSZEDWOHUGU
      HXJXGEWOXHWOJZXIWRJZKXIJZXCXIJZBXINZAXINZOXJXGXOXPXQXTCXHUIXOXHQJZXQXOYAX
      HUJCPCXHUKULZXHUMTXOXRABXIXIXOYAXAXIJZXBXIJZVDXRYBYAYCYDXRXAXBXHUOUPUQURU
      SXJXPWSXQWTXTXFXIDWRUTXIDKVAXSXEAXIDXRXDBXIDXIDXCVAVBVBVCVEVFTXGCXHVGDJEC
      VHVIZHIZDWPXGDYFXGYEWOJZDYFPZXGFGECYEDWSWTXFVJWSWTXFVKXGFLZDJZGLZDJZYIYKM
      ZDJZXFWSYJYLVDZYNVLWTYOXFYNXDYNYIXBMZDJABYIYKDDAFVMXCYPDXAYIXBVNVOBGVMYPY
      MDXBYKYIVPVOVQVRVSVTYEWAWBZWCWDXGYGYFWPJZXGYGYHYQWEXLWOHWFZWHYGYRVLXNWOQY
      SEWOQYBWGXMYSQPRQHWISWJWOYEHWKWLTWMWN $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d ph x y z $.  $d B x y z $.  $d F x y z $.  $d V x y z $.
    ismrcd.b $e |- ( ph -> B e. V ) $.
    ismrcd.f $e |- ( ph -> F : ~P B --> ~P B ) $.
    ismrcd.e $e |- ( ( ph /\ x C_ B ) -> x C_ ( F ` x ) ) $.
    ismrcd.m $e |- ( ( ph /\ x C_ B /\ y C_ x ) -> ( F ` y ) C_ ( F ` x ) ) $.
    ismrcd.i $e |- ( ( ph /\ x C_ B ) -> ( F ` ( F ` x ) ) = ( F ` x ) ) $.
    $( Any function from the subsets of a set to itself, which is extensive
       (satisfies ~ mrcssid ), isotone (satisfies ~ mrcss ), and idempotent
       (satisfies ~ mrcidm ) has a collection of fixed points which is a Moore
       collection, and itself is the closure operator for that collection.
       This can be taken as an alternate definition for the closure operators.
       This is the first half, ~ ismrcd2 is the second.  (Contributed by Stefan
       O'Rear, 1-Feb-2015.) $)
    ismrcd1 $p |- ( ph -> dom ( F i^i _I ) e. ( Moore ` B ) ) $=
      ( vz wss wceq syl wcel cfv wb syl2anc 3ad2ant1 cid cin cdm cpw inss1 dmss
      ax-mp fdm syl5sseq ssid elpwg mpbiri ffvelrn fvex elpw sylib wral sylan2b
      wf cv vex ralrimiva fveq2 sseq12d rcla4va eqssd wfn ffn fnelfp mpbird wne
      id c0 w3a cint wel wa wal cuni simp2 sstrd simp3 intssuni2 unipw syl6sseq
      cvv intex sylbi 3ad2ant3 adantr 3expib alrimiv sselda intss1 adantl sseq1
      jca anbi2d sseq1d imbi12d cla4gv syl3c mpbid sseqtrd ssint sylibr ismred
      wi ) AEUAUBZUCZDLAEUCZXJDUDZXIEMXJXKMEUAUEXIEUFUGAXLXLEUSZXKXLNHXLXLEUHOU
      IZADXJPZDEQZDNZAXPDAXPXLPZXPDMAXMDXLPZXRHAXSDDMZDUJADFPXSXTRGDDFUKOULZXLX
      LDEUMSXPDDEUNUOUPAXSBUTZYBEQZMZBXLUQZDXPMZYAAYDBXLYBXLPZAYBDMZYDYBDBVAUOZ
      IURVBZYDYFBDXLYBDNZYBDYCXPYKVLYBDEVCVDVESVFAEXLVGZXSXOXQRAXMYLHXLXLEVHOZY
      AXLEDVISVJALUTZXJMZYNVMVKZVNZYNVOZXJPZYREQZYRNZYQYTYRYQYTYBMZBYNUQYTYRMYQ
      UUBBYNYQBLVPZVQZYTYCYBUUDYRXLPZYHCUTZYBMZVQZUUFEQZYCMZXHZCVRZYHYRYBMZVQZY
      TYCMZYQUUEUUCYQUUEYRDMZYQYRXLVSZDYQYNXLMYPYRUUQMYQYNXJXLAYOYPVTZAYOXJXLMY
      PXNTWAZAYOYPWBYNXLWCSDWDWEYPAUUEUUPRZYOYPYRWFPUUTYNWGYRDWFUKWHWIVJZWJYQUU
      LUUCAYOUULYPAUUKCAYHUUGUUJJWKWLTWJUUDYHUUMUUDYGYHYQYNXLYBUUSWMZYIUPUUCUUM
      YQYBYNWNWOWQUUKUUNUUOXHCYRXLUUFYRNZUUHUUNUUJUUOUVCUUGUUMYHUUFYRYBWPWRUVCU
      UIYTYCUUFYREVCWSWTXAXBUUDYBXJPZYCYBNZYQYNXJYBUURWMUUDYLYGUVDUVERYQYLUUCAY
      OYLYPYMTZWJUVBXLEYBVISXCXDVBBYTYNXEXFYQUUEYEYRYTMZUVAAYOYEYPYJTYDUVGBYRXL
      YBYRNZYBYRYCYTUVHVLYBYREVCVDVESVFYQYLUUEYSUUARUVFUVAXLEYRVISVJXG $.
      $( [1-Feb-2015] $)

    $( Second half of ~ ismrcd1 .  (Contributed by Stefan O'Rear,
       1-Feb-2015.) $)
    ismrcd2 $p |- ( ph -> F = ( mrCls ` dom ( F i^i _I ) ) ) $=
      ( vz cfv wcel wa wss adantr wi cvv wceq cpw cid cin cdm cmrc wfn ffn cmre
      wf syl ismrcd1 eqid mrcf 3syl cv mrcssv elpwi mrcssid syl2an wal alrimivv
      3expib vex weq wb sseq1 adantl sseq12 anbi12d fveq2 imbi12d cla42gv mp2an
      fvex mp2and mrccl elpw sylibr fnelfp syl2anc mpbid sseqtrd anbi2d sseq12d
      chvarv sylan2 fveq2d eqeq12d ffvelrn sylan mpbird mrcsscl syl3anc eqfnfvd
      id eqssd ) ALDUAZEEUBUCUDZUEMZAWQWQEUIZEWQUFZHWQWQEUGUJZAWRDUHMNZWQWRWSUI
      WSWQUFABCDEFGHIJKUKZWRWSDWSULZUMWQWRWSUGUNALUOZWQNZOZXFEMZXFWSMZXHXIXJEMZ
      XJXHXJDPZXFXJPZXIXKPZAXLXGAXCXLXDWRXFWSDXEUPUJZQAXCXFDPZXMXGXDXFDUQZWRXFW
      SDXEURUSAXLXMOZXNRZXGABUOZDPZCUOZXTPZOZYBEMZXTEMZPZRZBUTCUTZXSAYHCBAYAYCY
      GJVBVAXFSNXJSNYIXSRLVCXFWSVNZYHXSCBXFXJSSCLVDZXTXJTZOZYDXRYGXNYMYAXLYCXMY
      LYAXLVEYKXTXJDVFVGYBXFXTXJVHVIYKYEXITYFXKTYGXNVEYLYBXFEVJXTXJEVJYEXIYFXKV
      HUSVKVLVMUJQVOXHXJWRNZXKXJTZAXCXPYNXGXDXQWRXFWSDXEVPUSXHXAXJWQNZYNYOVEAXA
      XGXBQZAYPXGAXLYPXOXJDYJVQVRQWQEXJVSVTWAWBXHXCXFXIPZXIWRNZXJXIPAXCXGXDQXGA
      XPYRXQAYAOZXTYFPZRAXPOZYRRBLBLVDZYTUUBUUAYRUUCYAXPAXTXFDVFWCZUUCXTXFYFXIU
      UCWOXTXFEVJZWDVKIWEWFXHYSXIEMZXITZXGAXPUUGXQYTYFEMZYFTZRUUBUUGRBLUUCYTUUB
      UUIUUGUUDUUCUUHUUFYFXIUUCYFXIEUUEWGUUEWHVKKWEWFXHXAXIWQNZYSUUGVEYQAWTXGUU
      JHWQWQXFEWIWJWQEXIVSVTWKWRXFWSXIDXEWLWMWPWN $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d B x y z $.  $d ph x y z $.  $d F x y z $.  $d J x y $.  $d V x y z $.
    istopclsd.b $e |- ( ph -> B e. V ) $.
    istopclsd.f $e |- ( ph -> F : ~P B --> ~P B ) $.
    istopclsd.e $e |- ( ( ph /\ x C_ B ) -> x C_ ( F ` x ) ) $.
    istopclsd.i $e |- ( ( ph /\ x C_ B ) -> ( F ` ( F ` x ) ) = ( F ` x ) ) $.
    istopclsd.z $e |- ( ph -> ( F ` (/) ) = (/) ) $.
    istopclsd.u $e |- ( ( ph /\ x C_ B /\ y C_ B ) -> ( F ` ( x u. y ) ) =
          ( ( F ` x ) u. ( F ` y ) ) ) $.
    istopclsd.j $e |- J = { z e. ~P B | ( F ` ( B \ z ) ) = ( B \ z ) } $.
    $( A closure function which satisfies ~ sscls , ~ clsidm , ~ cls0 , and
       ~ clsun defines a (unique) topology which it is the closure function
       on.  (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    istopclsd $p |- ( ph -> ( J e. ( TopOn ` B ) /\ ( cls ` J ) = F ) ) $=
      ( cfv wcel wceq syl wss ctopon ccl cv cdif cid cin cdm cpw crab wa wfn wb
      ffn adantr difss elpw2g mpbiri fnelfp syl2anc bicomd rabbidva syl5eq ccld
      wf w3a cun simp1 simp2 simp3 sstrd syl3anc ssequn2 biimpi 3ad2ant3 fveq2d
      eqtr3d sylibr ismrcd1 c0 0elpw sylancl inss1 dmss ax-mp syl5sseq 3ad2ant1
      mpbird fdm sseldd elpwi mpbid uneq12d eqtrd unssd vex unex mretopd simpld
      elpw eqid eqeltrd cmrc simprd eqtr4d ismrcd2 ctop istopon mrccls 3eqtr4rd
      cuni simplbi jca ) AGEUAPZQZGUBPZFRAGEDUCZUDZFUEUFZUGZQZDEUHZUIZXMAGXQFPX
      QRZDYAUIYBOAYCXTDYAAXPYAQZUJZXTYCYEFYAUKZXQYAQZXTYCULAYFYDAYAYAFVDZYFJYAY
      AFUMSZUNAYGYDAYGXQETZEXPUOAEHQYGYJULIXQEHUPSUQUNYAFXQURUSUTVAVBZAYBXMQZXS
      YBVCPZRZABCDEYBXSABCEFHIJKABUCZETZCUCZYOTZVEZYOFPZYQFPZVFZYTRUUAYTTYSYOYQ
      VFZFPZUUBYTYSAYPYQETZUUDUUBRZAYPYRVGAYPYRVHZYSYQYOEAYPYRVIUUGVJNVKYSUUCYO
      FYRAUUCYORZYPYRUUHYQYOVLVMVNVOVPUUAYTVLVQZLVRAVSXSQZVSFPVSRZMAYFVSYAQUUJU
      UKULYIEVTYAFVSURWAWGAYOXSQZYQXSQZVEZUUCXSQZUUDUUCRZUUNUUDUUBUUCUUNAYPUUEU
      UFAUULUUMVGUUNYOYAQZYPUUNXSYAYOAUULXSYATUUMAFUGZXSYAXRFTXSUURTFUEWBXRFWCW
      DAYHUURYARJYAYAFWHSWEWFZAUULUUMVHZWIZYOEWJSZUUNYQYAQZUUEUUNXSYAYQUUSAUULU
      UMVIZWIZYQEWJSZNVKUUNYTYOUUAYQUUNUULYTYORZUUTUUNYFUUQUULUVGULAUULYFUUMYIW
      FZUVAYAFYOURUSWKUUNUUMUUAYQRZUVDUUNYFUVCUUMUVIULUVHUVEYAFYQURUSWKWLWMUUNY
      FUUCYAQZUUOUUPULUVHUUNUUCETUVJUUNYOYQEUVBUVFWNUUCEYOYQBWOCWOWPWSVQYAFUUCU
      RUSWGYBWTWQZWRXAZAXSXBPGVCPZXBPZFXOAXSUVMXBAXSYMUVMAYLYNUVKXCAGYBVCYKVOXD
      VOABCEFHIJKUUILXEAGXFQZXOUVNRAXNUVOUVLXNUVOGXJEREGXGXKSUVNGUVNWTXHSXIXL
      $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d F x y z w $.  $d B x y z w $.
    $( A function is a Moore closure operator iff it satisfies ~ mrcssid ,
       ~ mrcss , and ~ mrcidm .  (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    ismrc $p |- ( F e. ( mrCls " ( Moore ` B ) ) <-> ( B e. _V /\
          F : ~P B --> ~P B /\ A. x A. y ( ( x C_ B /\ y C_ x ) ->
              ( x C_ ( F ` x ) /\ ( F ` y ) C_ ( F ` x ) /\
                ( F ` ( F ` x ) ) = ( F ` x ) ) ) ) ) $=
      ( vz vw cmrc cfv wcel cvv cv wss wa wceq w3a wi wal syl wb adantr cmre wf
      cima cpw wrex wfun crn cuni wfn fnmrc fnfun ax-mp fvelima mpan elex eqid1
      mre1cl mrcf mresspw fss syl2anc mrcssid adantrr mrcss ancom2s mrcidm 3jca
      3expb alrimivv feq1 fveq1 sseq2d sseq12d fveq12d eqeq12d 3anbi123d imbi2d
      ex id 2albidv 3anbi23d syl5ibcom rexlimiv cid cin simp1 simp2 ssid 3simpb
      cdm imim2i 2alimi vex weq sseq1 sseq12 ancoms anbi12d fveq2 imbi12d mp2an
      fveq2d cla42gv 3ad2ant3 mpan2i imp simpld syl2anr 3impib simprd fvssunirn
      ismrcd2 ismrcd1 fndm sseqtr4i funfvima2 eqeltrd impbii ) DGCUAHZUCZIZCJIZ
      CUDZYCDUBZAKZCLZBKZYELZMZYEYEDHZLZYGDHZYJLZYJDHZYJNZOZPZBQAQZOZYAEKZGHZDN
      ZEXSUEZYSGUFZYAUUCGUAUGUHZUIZUUDUJUUEGUKULZEDXSGUMUNUUBYSEXSYTXSIZYBYCYCU
      UAUBZYIYEYEUUAHZLZYGUUAHZUUJLZUUJUUAHZUUJNZOZPZBQAQZOUUBYSUUHYBUUIUURUUHC
      YTIYBYTCUQCYTUORUUHYCYTUUAUBYTYCLUUIYTUUACUUAUPZURYTCUSYCYTYCUUAUTVAUUHUU
      QABUUHYIUUPUUHYIMUUKUUMUUOUUHYFUUKYHYTYEUUACUUSVBVCUUHYHYFUUMUUHYHYFUUMYT
      YGUUAYECUUSVDVHVEUUHYFUUOYHYTYEUUACUUSVFVCVGVRVIVGUUBUUIYDUURYRYBYCYCUUAD
      VJUUBUUQYQABUUBUUPYPYIUUBUUKYKUUMYMUUOYOUUBUUJYJYEYEUUADVKZVLUUBUULYLUUJY
      JYGUUADVKUUTVMUUBUUNYNUUJYJUUBUUJYJUUADUUBVSUUTVNUUTVOVPVQVTWAWBWCRYSDDWD
      WEWJZGHZXTYSEFCDJYBYDYRWFZYBYDYRWGZYSYTCLZMZYTYTDHZLZUVGDHZUVGNZYSUVEUVHU
      VJMZYSUVEYTYTLZUVKYTWHYRYBUVEUVLMZUVKPZYDYRYIYKYOMZPZBQAQZUVNYQUVPABYPUVO
      YIYKYMYOWIWKWLYTJIZUVRUVQUVNPEWMZUVSUVPUVNABYTYTJJAEWNZBEWNZMZYIUVMUVOUVK
      UWBYFUVEYHUVLUVTYFUVESZUWAYEYTCWOZTUWAUVTYHUVLSYGYTYEYTWPWQWRUWBYKUVHYOUV
      JUVTYKUVHSUWAUVTYEYTYJUVGUVTVSYEYTDWSZVMTUVTYOUVJSUWAUVTYNUVIYJUVGUVTYJUV
      GDUWEXBUWEVOTWRWTXCXARXDXEXFZXGZYSUVEFKZYTLZUWHDHZUVGLZYSYIYMPZBQAQZUVEUW
      IMZUWKPZYRYBUWMYDYQUWLABYPYMYIYKYMYOWGWKWLXDUVRUWHJIUWMUWOPUVSFWMUWLUWOAB
      YTUWHJJUVTBFWNZMZYIUWNYMUWKUWQYFUVEYHUWIUVTUWCUWPUWDTUWPUVTYHUWISYGUWHYEY
      TWPWQWRUWPYLUWJNYJUVGNYMUWKSUVTYGUWHDWSUWEYLUWJYJUVGWPXHWTXCXARXIZUVFUVHU
      VJUWFXJZXLYSUVAXSIZUVBXTIZYSEFCDJUVCUVDUWGUWRUWSXMUUDXSGWJZLUWTUXAPUUGXSU
      UEUXBUACXKUUFUXBUUENUJUUEGXNULXOXSUVAGXPXARXQXR $.
      $( [1-Feb-2015] $)
  $}

