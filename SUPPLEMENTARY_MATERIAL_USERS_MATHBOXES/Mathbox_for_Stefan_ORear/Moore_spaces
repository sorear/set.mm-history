$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Moore spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Moore mrCls ACS $.

  $( The class of Moore systems. $)
  cmre $a class Moore $.

  $( The class function generating Moore closures. $)
  cmrc $a class mrCls $.

  $( The class of algebraic closure (Moore) systems. $)
  cacs $a class ACS $.

  ${
    $d c f s x $.

    $( Define a _Moore collection_, which is a family of subsets of a base set
       which preserve arbitrary intersection.  Elements of a Moore collection
       are termed _closed_; Moore collections generalize the notion of
       closedness from topologies ( ~ cldmre ) and vector spaces ( ~ lssmre )
       to the most general setting in which such concepts make sense.
       Definition of Moore collection of sets in [Schechter] p. 78.

       See ~ ismre , ~ mresspw , ~ mre1cl and ~ mreintcl for the major
       properties of a Moore collection.  Note that a Moore collection uniquely
       determines its base set ( ~ mreuni ); as such the disjoint union of all
       Moore collections is sometimes considered as ` U. ran Moore ` ,
       justified by ~ mreunirn . $)
    df-mre $a |- Moore = ( x e. _V |-> { c e. ~P ~P x | ( x e. c /\
        A. s e. ~P c ( s =/= (/) -> |^| s e. c ) ) } ) $.

    $( Define the _Moore closure_ of a generating set, which is the smallest
       closed set containing all generating elements.  Definition of Moore
       closure in [Schechter] p. 79.  This generalizes topological closure
       ( ~ mrccls ) and linear span ( ~ mrclsp ). $)
    df-mrc $a |- mrCls = ( c e. U. ran Moore |-> ( x e. ~P U. c |->
        |^| { s e. c | x C_ s } ) ) $.

    $( An important subclass of Moore systems are those which can be
       interpreted as closure under some collection of operators of finite
       arity (the collection itself is not required to be finite).  These are
       termed _algebraic closure systems_; similar to definition (A) of an
       algebraic closure system in [Schechter] p. 84, but to avoid the
       complexity of an arbitrary mixed collection of functions of various
       arities (especially if the axiom of infinity ~ omex is to be avoided),
       we consider a single function defined on finite sets instead. $)
    df-acs $a |- ACS = { c | E. f ( f : ( ~P U. c i^i Fin ) --> ~P U. c /\
      A. x e. ~P U. c ( x e. c <-> ( f " ~P x ) C_ x ) ) } $.
  $}

  ${
    $d C c s x $.  $d X c s x $.  $d S c s x $.
    $( Property of being a Moore collection on some base set.  (Contributed by
       Stefan O'Rear, 30-Jan-2015.) $)
    ismre $p |- ( C e. ( Moore ` X ) <-> ( C C_ ~P X /\ X e. C /\
        A. s e. ~P C ( s =/= (/) -> |^| s e. C ) ) ) $=
      ( vc vx cmre wcel cvv cpw cv wi wral elex syl crab wceq pweq anbi1d pwex
      wa cfv wss c0 wne cint w3a cdm elfvdm 3ad2ant2 wel pweqd rabeqbidv df-mre
      eleq1 vex rabex a1i fvmpt3 eleq2d wb eleq2 imbi2d raleqbidv anbi12d elrab
      pwexg elpw2g 3anass syl6bbr 3bitrd pm5.21nii ) ABFUAZGZBHGZABIZUBZBAGZCJZ
      UCUDZVRUEZAGZKZCAIZLZUFZVMBFUGZGVNABFUHBWFMNVQVPVNWDBAMUIVNVMABDJZGZVSVTW
      GGZKZCWGIZLZTZDVOIZOZGZAWNGZVQWDTZTZWEVNVLWOAEBEDUJZWLTZDEJZIZIZOZWOHFHXB
      BPZXAWMDXDWNXFXCVOXBBQUKXFWTWHWLXBBWGUNRULECDUMXEHGXBHGXADXDXCXBEUOSSUPUQ
      URUSWPWSUTVNWMWRDAWNWGAPZWHVQWLWDWGABVAXGWJWBCWKWCWGAQXGWIWAVSWGAVTVAVBVC
      VDVEUQVNWSVPWRTWEVNWQVPWRVNVOHGWQVPUTBHVFAVOHVGNRVPVQWDVHVIVJVK $.
      $( [30-Jan-2015] $)

    $( The Moore collection generator is a well-behaved function.  (Contributed
       by Stefan O'Rear, 30-Jan-2015.) $)
    fnmre $p |- Moore Fn _V $=
      ( vx vc vs wel cv c0 wne cint wcel wi cpw wral wa crab cvv cmre wfn fnmpt
      df-mre pwex vex rabex a1i mprg ) ABDCEZFGUEHBEZIJCUFKLMZBAEZKZKZNZOIZPOQA
      OAOUKPOACBSRULUHOIUGBUJUIUHAUATTUBUCUD $.
      $( [30-Jan-2015] $)

    $( A Moore collection is a subset of the power of the base set; each closed
       subset of the system is actually a subset of the base.  (Contributed by
       Stefan O'Rear, 30-Jan-2015.) $)
    mresspw $p |- ( C e. ( Moore ` X ) -> C C_ ~P X ) $=
      ( vs cmre cfv wcel cpw wss cv c0 wne cint wi wral ismre simp1bi ) ABDEFAB
      GHBAFCIZJKQLAFMCAGNABCOP $.
      $( [30-Jan-2015] $)

    $( A Moore-closed subset is a subset.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mress $p |- ( ( C e. ( Moore ` X ) /\ S e. C ) -> S C_ X ) $=
      ( cmre cfv wcel wa cpw wss mresspw sselda elpwi syl ) ACDEFZBAFGBCHZFBCIN
      AOBACJKBCLM $.
      $( [31-Jan-2015] $)

    $( In any Moore collection the base set is closed.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    mre1cl $p |- ( C e. ( Moore ` X ) -> X e. C ) $=
      ( vs cmre cfv wcel cpw wss cv c0 wne cint wi wral ismre simp2bi ) ABDEFAB
      GHBAFCIZJKQLAFMCAGNABCOP $.
      $( [30-Jan-2015] $)

    $( A nonempty collection of closed sets has a closed intersection.
       (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    mreintcl $p |- ( ( C e. ( Moore ` X ) /\ S C_ C /\ S =/= (/) ) ->
        |^| S e. C ) $=
      ( vs cmre cfv wcel wss c0 wne w3a cpw cv cint wral elpw2g biimpar 3adant3
      wi ismre simp3bi 3ad2ant1 simp3 neeq1 inteq eleq1d imbi12d rcla4va 3impia
      wceq syl3anc ) ACEFZGZBAHZBIJZKBALZGZDMZIJZURNZAGZSZDUPOZUOBNZAGZUMUNUQUO
      UMUQUNBAULPQRUMUNVCUOUMACLHCAGVCACDTUAUBUMUNUOUCUQVCUOVEVBUOVESDBUPURBUJZ
      USUOVAVEURBIUDVFUTVDAURBUEUFUGUHUIUK $.
      $( [30-Jan-2015] $)

    $d I c s y $.  $d X y $.  $d C y $.
    $( A nonempty indexed intersection of closed sets is closed.  (Contributed
       by Stefan O'Rear, 1-Feb-2015.) $)
    mreiincl $p |- ( ( C e. ( Moore ` X ) /\ I =/= (/) /\
          A. y e. I S e. C ) -> |^|_ y e. I S e. C ) $=
      ( vs vc cmre cfv wcel c0 wne wral w3a ciin cv wceq wrex 3ad2ant3 wex cint
      cab dfiin2g wss simp1 uniiunlem ibi wi n0 hbra1 hbre1 hbab ax-17 hbne ra4
      hbim com12 elisset ra4e ex syl5 rexcom4 syl6ib syld abn0 exlimi sylbi imp
      syl6ibr 3adant1 mreintcl syl3anc eqeltrd ) BEHIJZDKLZCBJZADMZNZADCOZFPCQZ
      ADRZFUBZUAZBVQVNVSWCQVOAFDCBUCSVRVNWBBUDZWBKLZWCBJVNVOVQUEVQVNWDVOVQWDAFD
      CBBUFUGSVOVQWEVNVOVQWEVOAPDJZATVQWEUHZADUIWFWGAVQWEAVPADUJAGWBKWAAFGVTADU
      KULGPKJAUMUNUPWFVQWAFTZWEWFVQVPWHVQWFVPVPADUOUQWFVPVTFTZADRZWHVPWIWFWJFCB
      URWFWIWJWIADUSUTVAVTAFDVBVCVDWAFVEVIVFVGVHVJBWBEVKVLVM $.
      $( [1-Feb-2015] $)

    $( Two closed sets have a closed intersection.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    mreincl $p |- ( ( C e. ( Moore ` X ) /\ A e. C /\ B e. C ) ->
        ( A i^i B ) e. C ) $=
      ( cmre cfv wcel w3a cpr cint cin wceq intprg 3adant1 c0 simp1 prssi prnzg
      wss wne 3ad2ant2 mreintcl syl3anc eqeltrrd ) CDEFGZACGZBCGZHZABIZJZABKZCU
      FUGUJUKLUEABCCMNUHUEUICSZUIOTZUJCGUEUFUGPUFUGULUEABCQNUFUEUMUGABCRUACUIDU
      BUCUD $.
      $( [30-Jan-2015] $)

    $( Since the entire base set of a Moore collection is the greatest element
       of it, the base set can be recovered from a Moore collection by set
       union.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    mreuni $p |- ( C e. ( Moore ` X ) -> U. C = X ) $=
      ( cmre cfv wcel cpw wss cuni wceq mre1cl mresspw elpwuni biimpa syl2anc )
      ABCDEBAEZABFGZAHBIZABJABKOPQABLMN $.
      $( [30-Jan-2015] $)

    $( Two ways to express the notion of being a Moore collection on an
       unspecified base.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    mreunirn $p |- ( C e. U. ran Moore <-> C e. ( Moore ` U. C ) ) $=
      ( vx cmre crn cuni wcel cfv cv cvv wrex wfn wb fnmre fnunirn ax-mp mreuni
      fveq2d eleq2d ibir rexlimivw sylbi fvssunirn sseli impbii ) ACDEZFZAAEZCG
      ZFZUFABHZCGZFZBIJZUICIKUFUMLMBACINOULUIBIULUIULUHUKAULUGUJCAUJPQRSTUAUHUE
      ACUGUBUCUD $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d ph s $.  $d C s $.  $d X s $.
    ismred.ss $e |- ( ph -> C C_ ~P X ) $.
    ismred.ba $e |- ( ph -> X e. C ) $.
    ismred.in $e |- ( ( ph /\ s C_ C /\ s =/= (/) ) -> |^| s e. C ) $.
    $( Properties that determine a Moore collection.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    ismred $p |- ( ph -> C e. ( Moore ` X ) ) $=
      ( cpw wss wcel cv c0 wne cint wi wral cmre cfv vex elpw sylan2b ralrimiva
      3expia ismre syl3anbrc ) ABCHICBJDKZLMZUFNBJZOZDBHZPBCQRJEFAUIDUJUFUJJAUF
      BIZUIUFBDSTAUKUGUHGUCUAUBBCDUDUE $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d V a b c $.  $d X a b c $.

    $( The Moore collections of subsets of a space, viewed as a kind of subset
       of the power set, form a Moore collection in their own right on the
       power set.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    mremre $p |- ( X e. V -> ( Moore ` X ) e. ( Moore ` ~P X ) ) $=
      ( va vb vc wcel cpw wss cv mresspw a1i c0 wne w3a wb mpbird ismred sselda
      ssid wa cmre cfv vex elpw sylibr ssriv mpbiri cint cuni intssuni2 3adant1
      elpwg unipw syl6sseq elpw2g 3ad2ant1 wel wex n0 intss1 adantl simpr sstrd
      ex exlimdv syl5bi 3impia wral simp2 mre1cl ralrimiva elintg simp12 simpl2
      syl simpl3 mreintcl syl3anc cvv intex sylbi 3ad2ant3 ) BAFZBUAUBZBGZCWDWE
      GZHWCCWDWFCIZWDFWGWEHZWGWFFWGBJWGWECUCUDUEUFKWCWEBCWEWEHWCWESKWCBWEFBBHBS
      BBAULUGWCWHWGLMZNZWGUHZWEFZWKBHZWJWKWEUIZBWHWIWKWNHWCWGWEUJUKBUMUNWCWHWLW
      MOWIWKBAUOUPPQWCWGWDHZWINZWKBDWCWOWIWKWEHZWIDCUQZDURWCWOTZWQDWGUSWSWRWQDW
      SWRWQWSWRTZWKDIZWEWRWKXAHWSXAWGUTVAWTXAWDFZXAWEHWSWGWDXAWCWOVBRXABJVOVCVD
      VEVFVGWPBWKFZBXAFZDWGVHZWPXDDWGWPWRTXBXDWPWGWDXAWCWOWIVIRXABVJVOVKWCWOXCX
      EOWIDBWGAVLUPPWPXAWKHZXALMZNZXAUHZWKFZXIEIZFZEWGVHZXHXLEWGXHECUQZTZXKWDFX
      AXKHXGXLXHWGWDXKWCWOWIXFXGVMRXOXAWKXKWPXFXGXNVNXNWKXKHXHXKWGUTVAVCWPXFXGX
      NVPXKXABVQVRVKXGWPXJXMOZXFXGXIVSFXPXAVTEXIWGVSVLWAWBPQQ $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d W a $.  $d U a $.  $d B a $.
    lssmre.u $e |- U = ( LSubSp ` W ) $.
    lssmre.b $e |- B = ( Base ` W ) $.
    $( The subspaces of a module comprise a Moore system on the vectors of the
       module.  (Contributed by Stefan O'Rear, 31-Jan-2015.) $)
    lssmre $p |- ( W e. LMod -> U e. ( Moore ` B ) ) $=
      ( va clmod wcel cpw cv wss lssss vex elpw sylibr a1i ssrdv lss1 lssintcl
      wi ismred ) CGHZBAFUBFBAIZFJZBHZUDUCHZTUBUEUDAKUFBUDACEDLUDAFMNOPQBACEDRU
      DBCDSUA $.
      $( [31-Jan-2015] $)
  $}

  ${
    $d X a $.  $d J a $.
    cldmre.x $e |- X = U. J $.
    $( The closed sets of a topology comprise a Moore system on the points of
       the topology.  (Contributed by Stefan O'Rear, 31-Jan-2015.) $)
    cldmre $p |- ( J e. Top -> ( Clsd ` J ) e. ( Moore ` X ) ) $=
      ( va ctop wcel ccld cfv cldss2 topcld cv c0 wne cint intcld 3com23 ismred
      wss ) AEFZAGHZBDABCIABCJSDKZLMUATRUANTFUAAOPQ $.
      $( [31-Jan-2015] $)
  $}

