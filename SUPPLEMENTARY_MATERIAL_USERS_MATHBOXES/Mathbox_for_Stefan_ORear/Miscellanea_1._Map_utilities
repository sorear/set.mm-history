$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Miscellanea 1. Map utilities
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( rather old, potentially worth reproving $)
  ${
    constmap.1 $e |- A e. _V $.
    constmap.2 $e |- B e. _V $.
    constmap.3 $e |- C e. _V $.
    $( A constant (represented without dummy variables) is an element of a
       function set.

       _Note:  In the following development, we will be quite often quantifying
       over functions and points in N-dimensional space (which are equivalent
       to functions from an "index set").  Many of the following theorems exist
       to transfer standard facts about functions to elements of function
       sets._ (Contributed by Stefan O'Rear, 30-Aug-2014.) $)
    constmap $p |- ( B e. C -> ( A X. { B } ) e. ( C ^m A ) ) $=
      ( wcel csn cmap co cxp wss snssi mapss syl wf fconst snex elmap mpbir a1i
      sseldd ) BCGZBHZAIJZCAIJZAUDKZUCUDCLUEUFLBCMUDCAFDNOUGUEGZUCUHAUDUGPABEQU
      DAUGBRDSTUAUB $.
      $( [30-Aug-2014] $)
  $}

  ${
    mapco2.1 $e |- B e. _V $.
    mapco2.2 $e |- C e. _V $.
    mapco2.3 $e |- E e. _V $.
    $( Post-composition (renaming indexes) of a mapping viewed as a point.
       (Contributed by Stefan O'Rear, 5-Oct-2014.) $)
    mapco2 $p |- ( ( A e. ( B ^m C ) /\ D : E --> C ) -> ( A o. D ) e. ( B ^m E
        ) ) $=
      ( cmap co wcel wf wa ccom elmap fco sylanb sylibr ) ABCIJKZECDLZMEBADNZLZ
      UABEIJKSCBALTUBBCAFGOECBADPQBEUAFHOR $.
      $( [5-Oct-2014] $)
  $}

  ${
    $d a b c d $.
    $( Eliminate antecedent for mapping theorems: domain can be taken to be a
       set.  (Contributed by Stefan O'Rear, 8-Oct-2014.) $)
    elmapex1 $p |- ( A e. ( B ^m C ) -> B e. _V ) $=
      ( va vb vd vc cmap co wcel c0 wceq cvv n0i cdm wrel cv wa wf cab df-mpt2
      copab2 reldmoprab cmpt2 df-map eqtri dmeqi releqi mpbir ovprc1 nsyl2 ) AB
      CHIZJULKLBMJULANBCHHOZPDQZMJEQZMJRFQUOUNGQSGTZLRZDEFUBZOZPUQDEFUCUMUSHURH
      DEMMUPUDURDEGUEDEFMMUPUAUFUGUHUIUJUK $.
      $( [8-Oct-2014] $)
  $}

  ${
    $d A c $.  $d B c $.  $d C c $.
    $( A mapping always has some domain, even if the notional domain is a
       proper class.  Uses ~ ovprc2 inessentially.  (Contributed by Stefan
       O'Rear, 8-Oct-2014.) $)
    elmapex2 $p |- ( A e. ( B ^m C ) -> E. c A e. ( B ^m c ) ) $=
      ( cvv wcel cmap co cv wex wi wceq oveq2 eleq2d cla4egv wn ovprc2 elmapex1
      mpcom syl6bi pm2.61i ) CEFZABCGHZFZABDIZGHZFZDJZKUGUDDCEUECLUFUCAUECBGMNO
      UBPZUDABBGHZFZUHUIUCUJABCGQNBEFUKUHABBRUGUKDBEUEBLUFUJAUEBBGMNOSTUA $.
      $( [8-Oct-2014] $)
  $}

  ${
    $d A c $.  $d B c $.  $d C c $.
    $( A mapping is always a function (even if C is a proper class)
       (Contributed by Stefan O'Rear, 9-Oct-2014.) $)
    elmapfun $p |- ( A e. ( B ^m C ) -> Fun A ) $=
      ( vc cmap co wcel cv wex wfun elmapex2 wf cvv elmapex1 vex elmapg sylancl
      wb ibi syl ffun exlimiv ) ABCEFGABDHZEFGZDIAJZABCDKUDUEDUDUCBALZUEUDUFUDB
      MGUCMGUDUFRABUCNDOBUCAMMPQSUCBAUATUBT $.
      $( [9-Oct-2014] $)
  $}

  $( A mapping is a function, forward direction only with superfluous
     antecedent removed.  (Contributed by Stefan O'Rear, 10-Oct-2014.) $)
  elmapi $p |- ( ( C e. _V /\ A e. ( B ^m C ) ) -> A : C --> B ) $=
    ( cvv wcel cmap co wa wf simpr elmapex1 adantl simpl elmapg syl2anc mpbid
    wb ) CDEZABCFGEZHZSCBAIZRSJTBDEZRSUAQSUBRABCKLRSMBCADDNOP $.
    $( [10-Oct-2014] $)

  ${
    $d A a b c $.  $d B a b c $.  $d C a b c $.  $d D a b c $.  $d E a b c $.
    $( Renaming indexes in a tuple, with sethood as antecedents.  (Contributed
       by Stefan O'Rear, 9-Oct-2014.)  (The proof was shortened by Mario
       Carneiro, 1-Dec-2014.) $)
    mapco2g $p |- ( ( ( E e. _V /\ C e. _V ) /\ ( A e. ( B ^m C ) /\ D : E -->
        C ) ) -> ( A o. D ) e. ( B ^m E ) ) $=
      ( cvv wcel wa cmap co wf ccom elmapi ad2ant2lr simprr syl2anc wb elmapex1
      fco ad2antrl simpll elmapg mpbird ) EFGZCFGZHZABCIJGZECDKZHZHZADLZBEIJGZE
      BUKKZUJCBAKZUHUMUEUGUNUDUHABCMNUFUGUHOECBADSPUJBFGZUDULUMQUGUOUFUHABCRTUD
      UEUIUABEUKFFUBPUC $.
      $( [1-Dec-2014] $) $( [9-Oct-2014] $)
  $}

  $( A restricted mapping is a mapping.  (Contributed by Stefan O'Rear,
     9-Oct-2014.) $)
  elmapssres $p |- ( ( A e. ( B ^m C ) /\ D C_ C /\ C e. _V ) -> ( A |` D ) e.
      ( B ^m D ) ) $=
    ( cmap co wcel wss cvv w3a cres wf simp1 wb elmapex1 3ad2ant1 simp3 syl2anc
    elmapg mpbid simp2 fssres ssexg 3adant1 mpbird ) ABCEFGZDCHZCIGZJZADKZBDEFG
    ZDBUJLZUICBALZUGULUIUFUMUFUGUHMUIBIGZUHUFUMNUFUGUNUHABCOPZUFUGUHQBCAIISRTUF
    UGUHUACBDAUBRUIUNDIGZUKULNUOUGUHUPUFDCIUCUDBDUJIISRUE $.
    $( [9-Oct-2014] $)

  ${
    $d N a b c x $.  $d A a b c x $.  $d B a b c x $.  $d C a b c x $.
    $d D a b c x $.  $d M a b c x $.
    mapfzcons.1 $e |- M = ( N + 1 ) $.
    $( Extending a one-based mapping by adding a tuple at the end results in
       another mapping.  (Contributed by Stefan O'Rear, 10-Oct-2014.) $)
    mapfzcons $p |- ( ( N e. NN0 /\ A e. ( B ^m ( 1 ... N ) ) /\ C e. B ) -> (
        A u. { <. M , C >. } ) e. ( B ^m ( 1 ... M ) ) ) $=
      ( cn0 wcel c1 cfz co cmap caddc csn cun wf wceq cvv ovex cuz w3a c0 simp2
      cop cin wb elmapex1 3ad2ant2 elmapg sylancl mpbid wss wf1o f1osng sylancr
      simp3 f1of syl snssi 3ad2ant3 fss syl2anc fzp1disj 3ad2ant1 syl21anc cmin
      fun cz cfv 1z cc0 ax-1cn subidi fveq2i eqtr4i eleq2i biimpi fzsuc2 eqcomd
      nn0uz unidm a1i feq23d mpbird opeq1i sneqi uneq2i oveq2i eleq12i sylibr )
      EGHZABIEJKZLKHZCBHZUAZAEIMKZCUDZNZOZBIWPJKZLKZHZADCUDZNZOZBIDJKZLKZHWOXBW
      TBWSPZWOWLWPNZOZBBOZWSPZXHWOWLBAPZXIBWRPZWLXIUEUBQZXLWOWMXMWKWMWNUCWOBRHZ
      WLRHWMXMUFWMWKXPWNABWLUGUHZIEJSBWLARRUIUJUKWOXICNZWRPZXRBULZXNWOXIXRWRUMZ
      XSWOWPRHWNYAEIMSWKWMWNUPWPCRBUNUOXIXRWRUQURWNWKXTWMCBUSUTXIXRBWRVAVBWKWMX
      OWNIEGVCVDWLXIBBAWRVGVEWOXJXKWTBWSWOWTXJWOIVHHEIIVFKZTVIZHZWTXJQVJWKWMYDW
      NWKYDGYCEGVKTVIYCVTYBVKTIVLVMVNVOVPVQVDIEVRUOVSXKBQWOBWAWBWCUKWOXPWTRHXBX
      HUFXQIWPJSBWTWSRRUIUJWDXEWSXGXAXDWRAXCWQDWPCFWEWFWGXFWTBLDWPIJFWHWHWIWJ
      $.
      $( [10-Oct-2014] $)

    $( Recover prefix mapping from an extended mapping.  (Contributed by Stefan
       O'Rear, 10-Oct-2014.) $)
    mapfzcons1 $p |- ( ( N e. NN0 /\ A e. ( B ^m ( 1 ... N ) ) /\ C e. B ) -> (
        ( A u. { <. M , C >. } ) |` ( 1 ... N ) ) = A ) $=
      ( cn0 wcel c1 cfz co csn cun cres c0 wceq cdm cin eqtri syl5eq w3a cop wf
      cmap resundir wfn cvv ovex elmapi mpan fnresdm 3syl 3ad2ant2 caddc dmsnop
      ffn dmres sneqi ineq2i fzp1disj 3ad2ant1 wrel relres reldm0 ax-mp uneq12d
      wb sylibr un0 syl6eq ) EGHZABIEJKZUDKHZCBHZUAZADCUBLZMVLNAVLNZVPVLNZMZAAV
      PVLUEVOVSAOMAVOVQAVROVMVKVQAPZVNVMVLBAUCZAVLUFVTVLUGHVMWAIEJUHABVLUIUJVLB
      AUPVLAUKULUMVOVRQZOPZVROPZVOWBVLEIUNKZLZRZOWBVLVPQZRWGVPVLUQWHWFVLWHDLWFD
      CUODWEFURSUSSVKVMWGOPVNIEGUTVATVRVBWDWCVGVPVLVCVRVDVEVHVFAVIVJT $.
      $( [10-Oct-2014] $)

    $( A nonempty mapping has a prefix.  (Contributed by Stefan O'Rear,
       10-Oct-2014.) $)
    mapfzcons1cl $p |- ( ( N e. NN0 /\ A e. ( B ^m ( 1 ... M ) ) ) -> ( A |` (
        1 ... N ) ) e. ( B ^m ( 1 ... N ) ) ) $=
      ( cn0 wcel c1 cfz co cmap wa wss cres simpr caddc fzssp1 oveq2i syl6sseqr
      cvv adantr ovex a1i elmapssres syl3anc ) DFGZABHCIJZKJGZLZUHHDIJZUGMZUGTG
      ZAUJNBUJKJGUFUHOUFUKUHUFUJHDHPJZIJUGHDFQCUMHIERSUAULUIHCIUBUCABUGUJUDUE
      $.
      $( [10-Oct-2014] $)

    $( Recover added element from an extended mapping.  (Contributed by Stefan
       O'Rear, 10-Oct-2014.) $)
    mapfzcons2 $p |- ( ( N e. NN0 /\ A e. ( B ^m ( 1 ... N ) ) /\ C e. B ) -> (
        ( A u. { <. M , C >. } ) ` M ) = C ) $=
      ( cn0 wcel c1 cfz co csn cfv wfn cin c0 wceq cvv ovex caddc w3a wf elmapi
      cmap cop cun mpan ffn syl 3ad2ant2 eqeltri fnsn a1i sneqi ineq2i fzp1disj
      syl5eq 3ad2ant1 snid fvun2 syl112anc simp3 fvsng sylancr eqtrd ) EGHZABIE
      JKZUDKHZCBHZUAZDADCUELZUFMZDVKMZCVJAVGNZVKDLZNZVGVOOZPQZDVOHZVLVMQVHVFVNV
      IVHVGBAUBZVNVGRHVHVTIEJSABVGUCUGVGBAUHUIUJVPVJDCDEITKZRFEITSUKZULUMVFVHVR
      VIVFVQVGWALZOPVOWCVGDWAFUNUOIEGUPUQURVSVJDWBUSUMVGVOAVKDUTVAVJDRHVIVMCQWB
      VFVHVIVBDCRBVCVDVE $.
      $( [10-Oct-2014] $)
  $}

