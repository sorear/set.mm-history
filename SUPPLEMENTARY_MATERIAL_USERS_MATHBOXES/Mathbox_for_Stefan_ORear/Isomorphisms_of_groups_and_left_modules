$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Isomorphisms of groups and left modules
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c GrpIso LMIso ~=g ~=m $.

  $( The class of group isomorphism sets. $)
  cgim $a class GrpIso $.

  $( The class of left module isomorphism sets. $)
  clmim $a class LMIso $.

  $( The class of the group isomorphism relation. $)
  cgic $a class ~=g $.

  $( The class of the left module isomorphism relation. $)
  clmic $a class ~=m $.

  ${
    $d g s t w x y $.

    $( An isomorphism of groups is a homomorphism which is also a bijection,
       i.e. it preserves equality as well as the group operation. $)
    df-gim $a |- GrpIso = ( s e. Grp , t e. Grp |-> { g e. ( s GrpHom t ) |
        g : ( Base ` s ) -1-1-onto-> ( Base ` t ) } ) $.

    $( An isomorphism of modules is a homomorphism which is also a bijection,
       i.e. it preserves equality as well as the group and scalar
       operations. $)
    df-lmim $a |- LMIso = ( s e. LMod , t e. LMod |-> { g e. ( s LMHom t ) |
        g : ( Base ` s ) -1-1-onto-> ( Base ` t ) } ) $.

    $( Two groups are said to be isomorphic iff they are connected by at least
       one isomorphism.  Isomophic groups share all global group properties,
       but to relate local properties requires knowledge of a specific
       isomorphism. $)
    df-gic $a |- ~=g = { <. s , t >. | ( s GrpIso t ) =/= (/) } $.

    $( Two modules are said to be isomorphic iff they are connected by at least
       one isomorphism. $)
    df-lmic $a |- ~=m = { <. s , t >. | ( s LMIso t ) =/= (/) } $.
  $}

  ${
    $d A a b $.  $d B a b $.  $d E a b $.  $d F a b $.  $d X a b $.
    $d Y a b $.  $d V a b $.
    elovmpt2.d $e |- D = ( a e. A , b e. B |-> C ) $.
    elovmpt2.c $e |- C e. _V $.
    elovmpt2.e $e |- ( ( a = X /\ b = Y ) -> C = E ) $.
    $( Utility lemma for two-parameter classes.

       _EDITORIAL_: can simplify ~ isghm , ~ islmhm .  (Contributed by Stefan
       O'Rear, 21-Jan-2015.) $)
    elovmpt2 $p |- ( Y e. V -> ( F e. ( X D Y ) <-> ( X e. A /\ Y e. B /\ F e.
        E ) ) ) $=
      ( wcel wa eleq2i cvv wceq wal co w3a cop cxp cfv cdm elfvdm dmmpt2 bicomi
      df-ov 3imtr4i opelxpg biimpa sylan2 3simpa adantl gen2 eleq1d cla42gv mpi
      cv ovmpt2ga mpd3an3 eleq2d df-3an baibr bitrd pm5.21nd ) IGOZFHIDUAZOZHAO
      ZIBOZFEOZUBZVLVMPZVKVIHIUCZABUDZOZVPFVQDUEZOVQDUFZOZVKVSFVQDUGVJVTFHIDUJQ
      WBVSWAVRVQJKABCDLMUHQUIUKVIVSVPHIABGULUMUNVOVPVIVLVMVNUOUPVPVKVNVOVPVJEFV
      LVMEROZVJESVPCROZKTJTWCWDJKMUQWDWCJKHIABJVAHSKVAISPCERNURUSUTJKHIABCEDRNL
      VBVCVDVOVPVNVLVMVNVEVFVGVH $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d B x y $.  $d X x y $.  $d Y x y $.
    gimlem2.r $e |- Rel dom A $.
    gimlem2.i $e |- ( F e. ( x A y ) -> F e. ( x B y ) ) $.
    $( Proper class lemma for implications of two-parameter classes.
       _EDITORIAL_: use this for ~ lmghm . $)
    gimlem2 $p |- ( F e. ( X A Y ) -> F e. ( X B Y ) ) $=
      ( cvv wcel co wn c0 eleq2d cv wi wceq imbi12d oveq2 noel ovprc1 con4i weq
      mtbiri oveq1 vtoclg chvarv ovprc2 mpbiri pm2.61i mpcom ) FJKZEFGCLZKZEFGD
      LZKZUMUOUMMZUOENKEUAURUNNEFGCHUBOUEUCEAPZGCLZKZEUSGDLZKZQZUOUQQAFJUSFRZVA
      UOVCUQVEUTUNEUSFGCUFOVEVBUPEUSFGDUFOSGJKZVDEUSBPZCLZKZEUSVGDLZKZQZVDBGJVG
      GRZVIVAVKVCVMVHUTEVGGUSCTOVMVJVBEVGGUSDTOSIUGVFMZVDEUSUSCLZKZEUSUSDLZKZQZ
      VLVSBABAUDZVIVPVKVRVTVHVOEVGUSUSCTOVTVJVQEVGUSUSDTOSIUHVNVAVPVCVRVNUTVOEU
      SGCUIOVNVBVQEUSGDUIOSUJUKUGUL $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d F a b c $.  $d R a b c $.  $d S a b c $.  $d B a b c $.  $d C a b c $.
    $d V a b c $.
    isgim.b $e |- B = ( Base ` R ) $.
    isgim.c $e |- C = ( Base ` S ) $.
    $( An isomorphism of groups is a bijective homomorphism.  (Contributed by
       Stefan O'Rear, 21-Jan-2015.) $)
    isgim $p |- ( S e. V -> ( F e. ( R GrpIso S ) <-> ( F e. ( R GrpHom S ) /\
          F : B -1-1-onto-> C ) ) ) $=
      ( vc va vb wcel co cgrp cv wf1o cghm cbs cfv wceq cgim crab w3a wa df-gim
      ovex rabex oveq12 wb fveq2 syl6eqr syl2an rabeqbidv elovmpt2 f1oeq1 elrab
      f1oeq23 3anbi3i simp3 ghmgrp1 ad2antrl ghmgrp2 adantrr simpr 3jca impbid2
      ancoms ex syl5bb bitrd ) DFLZECDUAMLCNLZDNLZEABIOZPZICDQMZUBZLZUCZEVPLZAB
      EPZUDZNNJOZRSZKOZRSZVNPZIWCWEQMZUBUAVQEFCDJKKIJUEWGIWHWCWEQUFUGWCCTZWEDTZ
      UDWGVOIWHVPWCCWEDQUHWIWDATWFBTWGVOUIWJWIWDCRSAWCCRUJGUKWJWFDRSBWEDRUJHUKW
      DAWFBVNUQULUMUNVSVLVMWBUCZVKWBVRWBVLVMVOWAIEVPABVNEUOUPURVKWKWBVLVMWBUSVK
      WBWKVKWBUDVLVMWBVTVLVKWACDEUTVAVKVTVMWAVTVKVMCDEFVBVGVCVKWBVDVEVHVFVIVJ
      $.
      $( [21-Jan-2015] $)

    $( An isomorphism of groups is a bijection.  (Contributed by Stefan O'Rear,
       21-Jan-2015.) $)
    gimf1o $p |- ( ( F e. ( R GrpIso S ) /\ S e. V ) ->
          F : B -1-1-onto-> C ) $=
      ( wcel cgim co wf1o cghm wa isgim simpr syl6bi impcom ) DFIZECDJKIZABELZS
      TECDMKIZUANUAABCDEFGHOUBUAPQR $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d F a b $.  $d R a b $.  $d S a b $.  $d a b c $.
    $( An isomorphism of groups is a homomorphism.  (Contributed by Stefan
       O'Rear, 21-Jan-2015.) $)
    gimghm $p |- ( F e. ( R GrpIso S ) -> F e. ( R GrpHom S ) ) $=
      ( va vb vc cgim cghm cgrp cv cbs cfv wf1o crab df-gim reldmmpt2 wcel eqid
      co cvv wa wb vex isgim ax-mp simpl sylbi gimlem2 ) DEGHCABDEIIDJZKLZEJZKL
      ZFJMFUIUKHSZNGEFDOPCUIUKGSQZCUMQZUJULCMZUAZUOUKTQUNUQUBEUCUJULUIUKCTUJRUL
      RUDUEUOUPUFUGUH $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d ph y $.  $d ps x $.  $d F x y $.  $d B x y $.  $d A x y $.
    rexima.x $e |- ( x = ( F ` y ) -> ( ph <-> ps ) ) $.
    $( Existential quantification under an image in terms of the base set.
       _EDITORIAL_: usable in ~ fnwe2lem2 .  (Contributed by Stefan O'Rear,
       21-Jan-2015.) $)
    rexima $p |- ( ( F Fn A /\ B C_ A ) ->
        ( E. x e. ( F " B ) ph <-> E. y e. B ps ) ) $=
      ( wfn wss wa cv cfv cima fvex wcel wceq wrex fvelimab eqcom rexbii syl6bb
      wb adantl rexxfr2d ) GEIFEJKZABCDDLZGMZGFNZFUGGOUFCLZUIPUHUJQZDFRUJUHQZDF
      RDEFUJGSUKULDFUHUJTUAUBULABUCUFHUDUE $.
      $( [21-Jan-2015] $)

    $( Universal quantification under an image in terms of the base set.
       _EDITORIAL_: duplicates ~ raleqfn , but with interface consistency with
       ~ rexrn .  (Contributed by Stefan O'Rear, 21-Jan-2015.) $)
    ralima $p |- ( ( F Fn A /\ B C_ A ) ->
        ( A. x e. ( F " B ) ph <-> A. y e. B ps ) ) $=
      ( wfn wss wa cv cfv cima fvex wcel wceq wrex fvelimab eqcom rexbii syl6bb
      wb adantl ralxfr2d ) GEIFEJKZABCDDLZGMZGFNZFUGGOUFCLZUIPUHUJQZDFRUJUHQZDF
      RDEFUJGSUKULDFUHUJTUAUBULABUCUFHUDUE $.
      $( [21-Jan-2015] $)
  $}

  $( Images under a function never map nonempty sets to empty sets.
     _EDITORIAL_: usable in ~ fnwe2lem2 .  (Contributed by Stefan O'Rear,
     21-Jan-2015.) $)
  fnimaeq0 $p |- ( ( F Fn A /\ B C_ A ) ->
      ( ( F " B ) = (/) <-> B = (/) ) ) $=
    ( cima c0 wceq cdm cin wfn wa imadisj incom fndm sseq2d biimpar df-ss sylib
    wss syl5eq eqeq1d syl5bb ) CBDEFCGZBHZEFCAIZBARZJZBEFCBKUFUCBEUFUCBUBHZBUBB
    LUFBUBRZUGBFUDUHUEUDUBABACMNOBUBPQSTUA $.
    $( [21-Jan-2015] $)

  ${
    $d F a b c d $.  $d A a b c d $.  $d R a b c d $.  $d S a b c d $.
    $d B a b c d $.  $d V a b c d $.
    subgim.b $e |- B = ( Base ` R ) $.
    $( Behavior of subgroups under isomorphism.  (Contributed by Stefan O'Rear,
       21-Jan-2015.) $)
    subggim $p |- ( ( F e. ( R GrpIso S ) /\ S e. V /\ A C_ B ) ->
        ( A e. ( SubGrp ` R ) <-> ( F " A ) e. ( SubGrp ` S ) ) ) $=
      ( cgim co wcel wss w3a csubg cfv cima wa cghm gimghm 3ad2ant1 adantr ccnv
      simpr simpl2 ghmima syl3anc wceq cbs wf1 wf1o eqid gimf1o f1of1 syl simp3
      3adant3 f1imacnv syl2anc ghmpreima sylan eqeltrrd impbida ) ECDHIJZDFJZAB
      KZLZACMNZJZEAOZDMNJZVEVGPECDQIJZVGVCVIVEVJVGVBVCVJVDCDERSZTVEVGUBVBVCVDVG
      UCCDAEFUDUEVEVIPEUAVHOZAVFVEVLAUFZVIVEBDUGNZEUHZVDVMVBVCVOVDVBVCPBVNEUIVO
      BVNCDEFGVNUJUKBVNEULUMUOVBVCVDUNBVNAEUPUQTVEVJVIVLVFJVKCDEVHURUSUTVA $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d F a b $.  $d S a b $.  $d T a b $.  $d W a b $.  $d X a b $.
    $d Y a b $.
    lmhmf1o.x $e |- X = ( Base ` S ) $.
    lmhmf1o.y $e |- Y = ( Base ` T ) $.
    $( A bijective module homomorphism is also converse homomorphic.
       (Contributed by Stefan O'Rear, 25-Jan-2015.) $)
    lmhmf1o $p |- ( ( F e. ( S LMHom T ) /\ T e. W ) ->
        ( F : X -1-1-onto-> Y <-> `' F e. ( T LMHom S ) ) ) $=
      ( va vb co wcel wa clmod cfv wceq cbs adantr eqid syl wf1o ccnv cghm csca
      clmhm cv cvsca wral w3a lmhmlmod2 lmhmlmod1 jca wb lmghm c0g ghmf1o sylan
      biimpa lmhmsca eqcomd simplll simpllr fveq2d eleq2d biimpar f1ocnv adantl
      adantrr wf f1of ffvelrn adantrl lmhmlin syl22anc f1ocnvfv2 ad2ant2l eqtrd
      oveq2d wi simplr lmodvscl syl3anc f1ocnvfv syl2anc ralrimivva 3jca islmhm
      mpd mpbir2and wfn lmhmf ffn id syl2anr dff1o4 sylanbrc impbida ) CABUEKLZ
      BDLZMZEFCUAZCUBZBAUEKLZWTXAMZXCBNLZANLZMZXBBAUCKLZAUDOZBUDOZPZIUFZJUFZBUG
      OZKZXBOXLXMXBOZAUGOZKZPZJFUHIXJQOZUHZUIZXDXEXFWTXEXAABCDUJRWTXFXAWRXFWSAB
      CUKRZRZULXDXHXKYAWTXAXHWRCABUCKLWSXAXHUMABCUNABBUOOZCDEFAUOOZGHYFSYESUPUQ
      URWTXKXAWTXJXIABCXIXJDXISZXJSZUSUTRZXDXSIJXTFXDXLXTLZXMFLZMZMZXRCOZXOPZXS
      YMYNXLXPCOZXNKZXOYMWRWSXLXIQOZLZXPELZYNYQPWRWSXAYLVAWRWSXAYLVBXDYJYSYKXDY
      SYJXDYRXTXLXDXIXJQYIVCVDVEVHZXDYKYTYJXDFEXBVIZYKYTXAUUBWTXAFEXBUAUUBEFCVF
      FEXBVJTVGFEXMXBVKUQVLZYRABECXIXQXNDXLXPYGYRSZGXQSZXNSZVMVNYMYPXMXLXNXAYKY
      PXMPWTYJEFXMCVOVPVRVQYMXAXRELZYOXSVSWTXAYLVTYMXFYSYTUUGXDXFYLYDRUUAUUCXLX
      QXIYREAXPGYGUUEUUDWAWBEFXRXOCWCWDWHWEWFXDXFXCXGYBMUMYDIJXTBAFXBXJXIXNXQNY
      HYGXTSHUUFUUEWGTWIWTXCMZCEWJZXBFWJZXAWTUUIXCWTEFCVIUUIEFABCDGHWKEFCWLTRUU
      HUUBUUJXCXCXFUUBWTXCWMYCFEBAXBNHGWKWNFEXBWLTEFCWOWPWQ $.
      $( [25-Jan-2015] $)
  $}

  $( The converse of a bijective group homomorphism is a bijective group
     homomorphism.  (Contributed by Stefan O'Rear, 25-Jan-2015.) $)
  gimcnv $p |- ( ( F e. ( S GrpIso T ) /\ T e. W ) ->
      `' F e. ( T GrpIso S ) ) $=
    ( cgim co wcel wa ccnv cghm cbs cfv wf1o eqid gimf1o wb gimghm c0g syl cgrp
    ghmf1o sylan mpbid f1ocnv ghmgrp1 adantr isgim mpbir2and ) CABEFGZBDGZHZCIZ
    BAEFGZULBAJFGZBKLZAKLZULMZUKUPUOCMZUNUPUOABCDUPNZUONZOZUICABJFGZUJURUNPABCQ
    ZABBRLZCDUPUOARLZUSUTVENVDNUAUBUCUKURUQVAUPUOCUDSUKATGZUMUNUQHPUIVFUJUIVBVF
    VCABCUESUFUOUPBAULTUTUSUGSUH $.
    $( [25-Jan-2015] $)

  ${
    $d O a b $.  $d A a b $.  $d B a b $.
    brwitnlem.r $e |- R = { <. a , b >. | ( a O b ) =/= (/) } $.
    brwitnlem.o $e |- Rel dom O $.
    $( Lemma for relations which assert the existence of a witness in a
       two-parameter set. $)
    brwitnlem $p |- ( A R B <-> ( A O B ) =/= (/) ) $=
      ( wbr cvv wcel co c0 wne cv wb wceq wa oveq12 neeq1d relopabi brrelexi wn
      ovprc1 necon1ai brabga anidms brprc ovprc2 syl5ibrcom pm2.61dan pm5.21nii
      bibi12d imp ) ABCIZAJKZABDLZMNZABCEOZFOZDLZMNZEFCGUAUBUPUQMABDHUDUEUPBJKZ
      UOURPZVBUREFABCJJUSAQZUTBQRVAUQMUSAUTBDSTGUFUPVCUCZVDUPVDVFAACIZAADLZMNZP
      ZUPVJVBVIEFAACJJVEUTAQRVAVHMUSAUTADSTGUFUGVFUOVGURVIABCUHVFUQVHMABDUITUMU
      JUNUKUL $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d R f r s $.  $d S f r s $.  $d F f r s $.  $d B f $.  $d C f $.
    $d V f $.
    $( The relation "is isomorphic to" for groups.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    brgic $p |- ( R ~=g S <-> ( R GrpIso S ) =/= (/) ) $=
      ( vr vs vf cgic cgim df-gic cgrp cv cbs cfv wf1o cghm co df-gim reldmmpt2
      crab brwitnlem ) ABFGCDDCHCDIICJZKLDJZKLEJMETUANORGDECPQS $.
      $( [25-Jan-2015] $)

    $( Prove isomorphic by an explicit isomorphism.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    brgici $p |- ( F e. ( R GrpIso S ) -> R ~=g S ) $=
      ( cgim co wcel c0 wne cgic wbr ne0i brgic sylibr ) CABDEZFNGHABIJNCKABLM
      $.
      $( [25-Jan-2015] $)

    $( Isomorphism implies the left side is a group.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    giclcl $p |- ( R ~=g S -> R e. Grp ) $=
      ( vf cgic wbr cv cgim co wcel wex cgrp c0 wne brgic n0 bitri cghm ghmgrp1
      gimghm syl exlimiv sylbi ) ABDEZCFZABGHZIZCJZAKIZUCUELMUGABNCUEOPUFUHCUFU
      DABQHIUHABUDSABUDRTUAUB $.
      $( [25-Jan-2015] $)

    gicen.b $e |- B = ( Base ` R ) $.
    gicen.c $e |- C = ( Base ` S ) $.
    $( Isomorphic groups have equinumerous base sets.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    gicen $p |- ( ( R ~=g S /\ S e. V ) -> B ~~ C ) $=
      ( vf cgic wbr wcel cen cgim co c0 wne wi brgic cbs sylbi cv wex n0 gimf1o
      wa wf1o cfv cvv fvex eqeltri f1oen syl ex exlimiv imp ) CDIJZDEKZABLJZUPC
      DMNZOPZUQURQZCDRUTHUAZUSKZHUBVAHUSUCVCVAHVCUQURVCUQUEABVBUFURABCDVBEFGUDA
      BVBACSUGUHFCSUIUJUKULUMUNTTUO $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d R a b c $.  $d S a b c $.  $d V a b c $.
    $( A less trivial example of a group invariant: cardinality of the subgroup
       lattice.  (Contributed by Stefan O'Rear, 25-Jan-2015.) $)
    gicsubgen $p |- ( ( R ~=g S /\ S e. V ) -> ( SubGrp ` R ) ~~
        ( SubGrp ` S ) ) $=
      ( va vb vc wbr wcel csubg cfv cv co cima cvv imaexg ax-mp a1ii wceq cbs
      wa cgic cen cgim wex wi c0 wne brgic bitri ccnv fvex a1i vex cnvex gimghm
      n0 cghm ad2antrr simpr simplr ghmima syl3anc wf1 wss wf1o eqid gimf1o syl
      f1of1 subgss f1imacnv syl2an eqcomd imaeq2 eqeq2d anbi12d syl5ibrcom impr
      jca eleq1 adantr ghmpreima sylan wfo f1ofo foimacnv impbida en2d ex sylbi
      exlimiv imp ) ABUAGZBCHZAIJZBIJZUBGZWMDKZABUCLZHZDUDZWNWQUEZWMWSUFUGXAABU
      HDWSUPUIWTXBDWTWNWQWTWNTZEFWOWPWREKZMZWRUJZFKZMZWONHXCAIUKULXCXDWOHZXENHZ
      WRNHXJDUMZWRXDNOPQXCXGWPHZXHNHZXFNHXMWRXKUNXFXGNOPQXCXIXGXERZTZXLXDXHRZTZ
      XCXIXNXQXCXITZXQXNXEWPHZXDXFXEMZRZTXRXSYAXRWRABUQLHZXIWNXSWTYBWNXIABWRUOZ
      URXCXIUSWTWNXIUTABXDWRCVAVBXRXTXDXCASJZBSJZWRVCZXDYDVDXTXDRXIXCYDYEWRVEZY
      FYDYEABWRCYDVFZYEVFZVGZYDYEWRVIVHYDXDAYHVJYDYEXDWRVKVLVMVSXNXLXSXPYAXGXEW
      PVTXNXHXTXDXGXEXFVNVOVPVQVRXCXLXPXOXCXLTZXOXPXHWOHZXGWRXHMZRZTYKYLYNXCYBX
      LYLWTYBWNYCWAABWRXGWBWCYKYMXGXCYDYEWRWDZXGYEVDYMXGRXLXCYGYOYJYDYEWRWEVHYE
      XGBYIVJYDYEXGWRWFVLVMVSXPXIYLXNYNXDXHWOVTXPXEYMXGXDXHWRVNVOVPVQVRWGWHWIWK
      WJWL $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d F a b c $.  $d R a b c $.  $d S a b c $.  $d B a b c $.  $d C a b c $.
    $d V a b c $.
    islmim.b $e |- B = ( Base ` R ) $.
    islmim.c $e |- C = ( Base ` S ) $.
    $( An isomorphism of left modules is a bijective homomorphism.
       (Contributed by Stefan O'Rear, 21-Jan-2015.) $)
    islmim $p |- ( S e. V -> ( F e. ( R LMIso S ) <-> ( F e. ( R LMHom S ) /\
          F : B -1-1-onto-> C ) ) ) $=
      ( vc va vb wcel co clmod cv wf1o clmhm cbs cfv wceq clmim crab wa df-lmim
      w3a ovex rabex oveq12 wb syl6eqr f1oeq23 syl2an rabeqbidv elovmpt2 f1oeq1
      fveq2 elrab 3anbi3i simp3 lmhmlmod1 ad2antrr lmhmlmod2 adantlr simpl 3jca
      expcom impbid2 syl5bb bitrd ) DFLZECDUAMLCNLZDNLZEABIOZPZICDQMZUBZLZUEZEV
      OLZABEPZUCZNNJOZRSZKOZRSZVMPZIWBWDQMZUBUAVPEFCDJKKIJUDWFIWGWBWDQUFUGWBCTZ
      WDDTZUCWFVNIWGVOWBCWDDQUHWHWCATWEBTWFVNUIWIWHWCCRSAWBCRUPGUJWIWEDRSBWDDRU
      PHUJWCAWEBVMUKULUMUNVRVKVLWAUEZVJWAVQWAVKVLVNVTIEVOABVMEUOUQURVJWJWAVKVLW
      AUSWAVJWJWAVJUCVKVLWAVSVKVTVJCDEUTVAVSVJVLVTCDEFVBVCWAVJVDVEVFVGVHVI $.
      $( [21-Jan-2015] $)

    $( An isomorphism of left modules is a bijection.  (Contributed by Stefan
       O'Rear, 21-Jan-2015.) $)
    lmimf1o $p |- ( ( F e. ( R LMIso S ) /\ S e. V ) ->
          F : B -1-1-onto-> C ) $=
      ( wcel clmim co wf1o clmhm wa islmim simpr syl6bi impcom ) DFIZECDJKIZABE
      LZSTECDMKIZUANUAABCDEFGHOUBUAPQR $.
      $( [21-Jan-2015] $)
  $}

  ${
    $d F a b $.  $d R a b $.  $d S a b $.  $d a b c $.
    $( An isomorphism of modules is a homomorphism.  (Contributed by Stefan
       O'Rear, 21-Jan-2015.) $)
    lmimlmhm $p |- ( F e. ( R LMIso S ) -> F e. ( R LMHom S ) ) $=
      ( va vb vc clmim clmhm clmod cbs cfv wf1o crab df-lmim reldmmpt2 wcel cvv
      cv co eqid wa wb vex islmim ax-mp simplbi gimlem2 ) DEGHCABDEIIDRZJKZERZJ
      KZFRLFUHUJHSZMGEFDNOCUHUJGSPZCULPZUIUKCLZUJQPUMUNUOUAUBEUCUIUKUHUJCQUITUK
      TUDUEUFUG $.
      $( [21-Jan-2015] $)

    $( An isomorphism of modules is an isomorphism of groups.  (Contributed by
       Stefan O'Rear, 21-Jan-2015.) $)
    lmimgim $p |- ( F e. ( R LMIso S ) -> F e. ( R GrpIso S ) ) $=
      ( va vb vc clmim cgim clmod cv cbs cfv wf1o co wcel wa cvv wb eqid ax-mp
      clmhm crab df-lmim reldmmpt2 cghm lmghm anim1i vex islmim 3imtr4i gimlem2
      isgim ) DEGHCABDEIIDJZKLZEJZKLZFJMFUMUOUANZUBGEFDUCUDCUQOZUNUPCMZPZCUMUOU
      ENOZUSPZCUMUOGNOZCUMUOHNOZURVAUSUMUOCUFUGUOQOZVCUTREUHZUNUPUMUOCQUNSZUPSZ
      UITVEVDVBRVFUNUPUMUOCQVGVHULTUJUK $.
      $( [21-Jan-2015] $)
  $}

  $( The converse of a bijective module homomorphism is a bijective module
     homomorphism.  (Contributed by Stefan O'Rear, 25-Jan-2015.) $)
  lmimcnv $p |- ( ( F e. ( S LMIso T ) /\ T e. W ) ->
      `' F e. ( T LMIso S ) ) $=
    ( clmim co wcel wa ccnv clmhm cbs cfv wf1o eqid lmimf1o wb lmimlmhm lmhmf1o
    syl clmod sylan mpbid f1ocnv lmhmlmod1 adantr islmim mpbir2and ) CABEFGZBDG
    ZHZCIZBAEFGZUKBAJFGZBKLZAKLZUKMZUJUOUNCMZUMUOUNABCDUONZUNNZOZUHCABJFGZUIUQU
    MPABCQZABCDUOUNURUSRUAUBUJUQUPUTUOUNCUCSUJATGZULUMUPHPUHVCUIUHVAVCVBABCUDSU
    EUNUOBAUKTUSURUFSUG $.
    $( [25-Jan-2015] $)

  ${
    $d R f r s $.  $d S f r s $.  $d F f r s $.
    $( The relation "is isomorphic to" for modules.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    brlmic $p |- ( R ~=m S <-> ( R LMIso S ) =/= (/) ) $=
      ( vr vs vf clmic clmim df-lmic clmod cv cbs cfv wf1o clmhm crab reldmmpt2
      co df-lmim brwitnlem ) ABFGCDDCHCDIICJZKLDJZKLEJMETUANQOGDECRPS $.
      $( [25-Jan-2015] $)

    $( Prove isomorphic by an explicit isomorphism.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    brlmici $p |- ( F e. ( R LMIso S ) -> R ~=m S ) $=
      ( clmim co wcel c0 wne clmic wbr ne0i brlmic sylibr ) CABDEZFNGHABIJNCKAB
      LM $.
      $( [25-Jan-2015] $)

    $( Isomorphism implies the left side is a module.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    lmiclcl $p |- ( R ~=m S -> R e. LMod ) $=
      ( vf clmic wbr cv clmim co wcel wex clmod c0 wne brlmic n0 bitri lmimlmhm
      clmhm lmhmlmod1 syl exlimiv sylbi ) ABDEZCFZABGHZIZCJZAKIZUCUELMUGABNCUEO
      PUFUHCUFUDABRHIUHABUDQABUDSTUAUB $.
      $( [25-Jan-2015] $)
  $}


