$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Algebraic numbers are countable
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d H a b c d e f $.  $d R a b c d e f $.  $d S a b c d e f $.
    $d A a b c d e f $.  $d B a b c d e f $.

    $( Lemma for ~ isopo . $)
    isopolem $p |- ( H Isom R , S ( A , B ) -> ( S Po B -> R Po A ) ) $=
      ( va vb vc vd ve vf cv wbr wa wi wral wcel ex wb anbi12d wiso wpo w3a cfv
      wn wf1o isof1o f1of ffvelrn 3anim123d 3syl imp breq12 anidms notbid breq1
      wceq anbi1d imbi12d breq2 imbi1d anbi2d rcla43v syl simpl simpr1 syl12anc
      wf isorel simpr2 simpr3 sylibrd com23 imp31 ralrimivvva df-po 3imtr4g ) A
      BCDEUAZFLZVSDMZUEZVSGLZDMZWBHLZDMZNZVSWDDMZOZNZHBPGBPFBPZILZWKCMZUEZWKJLZ
      CMZWNKLZCMZNZWKWPCMZOZNZKAPJAPIAPZBDUBACUBVRWJXBVRWJNXAIJKAAAVRWJWKAQZWNA
      QZWPAQZUCZXAVRXFWJXAVRXFWJXAOVRXFNZWJWKEUDZXHDMZUEZXHWNEUDZDMZXKWPEUDZDMZ
      NZXHXMDMZOZNZXAXGXHBQZXKBQZXMBQZUCZWJXROVRXFYBVRABEUFABEVHZXFYBOABCDEUGAB
      EUHYCXCXSXDXTXEYAYCXCXSABWKEUIRYCXDXTABWNEUIRYCXEYAABWPEUIRUJUKULWIXRXJXH
      WBDMZWENZXHWDDMZOZNXJXLXKWDDMZNZYFOZNFGHXHXKXMBBBVSXHUQZWAXJWHYGYKVTXIYKV
      TXISVSXHVSXHDUMUNUOYKWFYEWGYFYKWCYDWEVSXHWBDUPURVSXHWDDUPUSTWBXKUQZYGYJXJ
      YLYEYIYFYLYDXLWEYHWBXKXHDUTWBXKWDDUPTVAVBWDXMUQZYJXQXJYMYIXOYFXPYMYHXNXLW
      DXMXKDUTVBWDXMXHDUTUSVBVCVDXGWMXJWTXQXGWLXIXGVRXCXCWLXISVRXFVEZVRXCXDXEVF
      ZYOABWKWKCDEVIVGUOXGWRXOWSXPXGWOXLWQXNXGVRXCXDWOXLSYNYOVRXCXDXEVJZABWKWNC
      DEVIVGXGVRXDXEWQXNSYNYPVRXCXDXEVKZABWNWPCDEVIVGTXGVRXCXEWSXPSYNYOYQABWKWP
      CDEVIVGUSTVLRVMVNVORFGHBDVPIJKACVPVQ $.
      $( [16-Nov-2014] $)

    $( An isomorphism preserves partial ordering.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    isopo $p |- ( H Isom R , S ( A , B ) -> ( R Po A <-> S Po B ) ) $=
      ( wiso wpo ccnv wi isocnv isopolem syl impbid ) ABCDEFZACGZBDGZNBADCEHZFO
      PIABCDEJBADCQKLABCDEKM $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ isoso . $)
    isosolem $p |- ( H Isom R , S ( A , B ) -> ( S Or B -> R Or A ) ) $=
      ( va vb vc vd wpo cv wbr weq w3o wral wa wor wcel wceq 3orbi123d isopolem
      wiso cfv wi wf1o wf isof1o f1of ffvelrn ex anim12d 3syl breq1 eqeq1 breq2
      imp eqeq2 rcla42v syl isorel wb f1of1 f1fveq sylan bicomd ancom2s sylibrd
      wf1 ralrimdvva df-so 3imtr4g ) ABCDEUBZBDJZFKZGKZDLZFGMZVOVNDLZNZGBOFBOZP
      ACJZHKZIKZCLZHIMZWCWBCLZNZIAOHAOZPBDQACQVLVMWAVTWHABCDEUAVLVTWGHIAAVLWBAR
      ZWCARZPZPZVTWBEUCZWCEUCZDLZWMWNSZWNWMDLZNZWGWLWMBRZWNBRZPZVTWRUDVLWKXAVLA
      BEUEZABEUFZWKXAUDABCDEUGZABEUHXCWIWSWJWTXCWIWSABWBEUIUJXCWJWTABWCEUIUJUKU
      LUPVSWRWMVODLZWMVOSZVOWMDLZNFGWMWNBBVNWMSVPXEVQXFVRXGVNWMVODUMVNWMVOUNVNW
      MVODUOTVOWNSXEWOXFWPXGWQVOWNWMDUOVOWNWMUQVOWNWMDUMTURUSWLWDWOWEWPWFWQABWB
      WCCDEUTWLWPWEVLABEVHZWKWPWEVAVLXBXHXDABEVBUSABWBWCEVCVDVEVLWJWIWFWQVAABWC
      WBCDEUTVFTVGVIUKFGBDVJHIACVJVK $.
      $( [16-Nov-2014] $)

    $( An isomorphism preserves strict ordering.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    isoso $p |- ( H Isom R , S ( A , B ) -> ( R Or A <-> S Or B ) ) $=
      ( wiso wor ccnv wi isocnv isosolem syl impbid ) ABCDEFZACGZBDGZNBADCEHZFO
      PIABCDEJBADCQKLABCDEKM $.
      $( [16-Nov-2014] $)

    $d a b c d e f x $.
    $( The complex numbers can be linearly ordered.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    cnso $p |- E. x x Or CC $=
      ( va vb vd vc ve cr cc cv wex wor wbr cn cnex cfv wceq wa clt wrex syl wb
      wf1o cen cpw nnex pwex rpnnen cpnnen entr4i bren mpbi copab cxp ltso wiso
      cin eqid f1oiso mpan2 isoso soinxp syl6bb mpbii xpex inex2 cla4ev exlimiv
      soeq1 ax-mp ) GHBIZUBZBJZHAIZKZAJZGHUCLVLGMUDHMUEUFUGUHUIGHBNUJUKVKVOBVKH
      CIDIZVJOPEIFIZVJOPQVPVQRLQFGSDGSCEULZHHUMZUPZKZVOVKGRKZWAUNVKGHRVRVJUOZWB
      WAUAVKVRVRPWCVRUQDFCEGHRVRVJURUSWCWBHVRKWAGHRVRVJUTHVRVAVBTVCVNWAAVTVSVRH
      HNNVDVEHVMVTVHVFTVGVI $.
      $( [16-Nov-2014] $)

    $d a b c F $.
    $( A well-ordering has no nontrivial automorphisms.  (Is sethood actually
       needed here?)  (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    weniso $p |- ( ( A e. V /\ R We A /\ F Isom R , R ( A , A ) ) ->
        F = ( _I |` A ) ) $=
      ( va vc vb wcel cvv wceq cfv wral wn wbr wa syl wi fveq2 id adantr wwe cv
      wiso cid cres elex w3a crab c0 wne wrex rabn0 rexnal bitri wfr wss simpl1
      rabexg simpl2 wefr ssrab2 a1i simpr fri syl22anc weq eqeq12d notbid elrab
      ex ralrab con34b bicomi ralbii wo simprr wor weso wf1o simpl3 isof1o f1of
      wb wf simprl ffvelrn syl2anc sotrieq syl12anc con2bid mpbird breq1 rcla4v
      imbi12d com23 imp wf1 f1fveq pm2.21 ad2antll sylbid syld ccnv f1ocnv 3syl
      f1of1 isorel f1ocnvfv2 breq1d bitr2d biimpa simplrr adantl fveq2d 3eqtr3d
      sylc jaodan syl5bi rexlimdv syl5bir pm2.18d fvresi eqeq2d biimprd ralimia
      mpdan wfn 3ad2ant3 f1ofn fnresi eqfnfv2 syl3an1 ) ADHAIHZABUAZAABBCUCZCUD
      AUEZJZADUFYMYNYOUGZYQEUBZCKZYSYPKZJZEALZYRYTYSJZEALZUUCYRUUEUUEMZUUDMZEAU
      HZUIUJZYRUUEUUIUUGEAUKUUFUUGEAULUUDEAUMUNYRUUIFUBZGUBZBNZMZFUUHLZGUUHUKZU
      UEYRUUIUUOYRUUIOZUUHIHZABUOZUUHAUPZUUIUUOUUPYMUUQYMYNYOUUIUQUUGEAIURPUUPY
      NUURYMYNYOUUIUSABUTPUUSUUPUUGEAVAVBYRUUIVCGFAUUHIBVDVEVJYRUUNUUEGUUHUUKUU
      HHUUKAHZUUKCKZUUKJZMZOZYRUUNUUEQZUUGUVCEUUKAEGVFZUUDUVBUVFYTUVAYSUUKYSUUK
      CRUVFSVGVHVIYRUVDUVEUUNUULUUJCKZUUJJZQZFALZYRUVDOZUUEUUNUVHMZUUMQZFALUVJU
      UGUVLUUMFEAEFVFZUUDUVHUVNYTUVGYSUUJYSUUJCRUVNSVGVHVKUVMUVIFAUVIUVMUULUVHV
      LVMVNUNUVKUVAUUKBNZUUKUVABNZVOZUVJUUEQZUVKUVQUVCYRUUTUVCVPUVKUVBUVQUVKABV
      QZUVAAHZUUTUVBUVQMWCUVKYNUVSYMYNYOUVDUSABVRPUVKAACWDZUUTUVTUVKAACVSZUWAUV
      KYOUWBYMYNYOUVDVTZAABBCWAZPZAACWBPYRUUTUVCWEZAAUUKCWFWGZUWFAUVAUUKBWHWIWJ
      WKUVKUVOUVRUVPUVKUVOOUVJUVACKZUVAJZUUEUVKUVOUVJUWIQUVKUVJUVOUWIUVKUVTUVJU
      VOUWIQZQUWGUVIUWJFUVAAUUJUVAJZUULUVOUVHUWIUUJUVAUUKBWLUWKUVGUWHUUJUVAUUJU
      VACRUWKSVGWNWMPWOWPUVKUWIUUEQUVOUVKUWIUVBUUEUVKAACWQZUVTUUTUWIUVBWCUVKUWB
      UWLUWEAACXFPUWGUWFAAUVAUUKCWRWIUVCUVBUUEQYRUUTUVBUUEWSZWTXATXBUVKUVPOZUVJ
      UUKCXCZKZCKZUWPJZUUEUWNUWPAHZUWPUUKBNZUVJUWRQUVKUWSUVPUVKAAUWOWDZUUTUWSUV
      KUWBAAUWOVSUXAUWEAACXDAAUWOWBXEUWFAAUUKUWOWFWGZTUVKUVPUWTUVKUWTUWQUVABNZU
      VPUVKYOUWSUUTUWTUXCWCUWCUXBUWFAAUWPUUKBBCXGWIUVKUWQUUKUVABUVKUWBUUTUWQUUK
      JZUWEUWFAAUUKCXHWGZXIXJXKUWSUVJUWTUWRUVIUWTUWRQFUWPAUUJUWPJZUULUWTUVHUWRU
      UJUWPUUKBWLUXFUVGUWQUUJUWPUUJUWPCRUXFSVGWNWMWOXPUVKUWRUUEQUVPUVKUWRUUEUVK
      UWROZUVCUVBUUEYRUUTUVCUWRXLUXGUWQCKZUWQUVAUUKUWRUXHUWQJUVKUWQUWPCRXMUVKUX
      HUVAJUWRUVKUWQUUKCUXEXNTUVKUXDUWRUXETXOUWMXPVJTXBXQYFXRVJXRXSXBXTYAUUDUUB
      EAYSAHZUUBUUDUXIUUAYSYTAYSYBYCYDYEPYRCAYGZYPAYGZYQUUCWCYRUWBUXJYOYMUWBYNU
      WDYHAACYIPUXKYRAYJVBEACYPYKWGWKYL $.
      $( [16-Nov-2014] $)

    $d a b c f R $.  $d f A $.
    $( A finite totally ordered set has a unique order isomorphism to a finite
       ordinal.  (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    finnisoeu $p |- ( ( R Or A /\ A e. Fin ) ->
        E! f f Isom _E , R ( ( card ` A ) , A ) ) $=
      ( va cfn wcel ccrd cep wiso con0 syl wceq cen wbr wf1o isof1o adantl ccom
      wa a1i wor cfv cv wex weq wi wal weu wrex wreu simpr wofi ordtype syl2anc
      wwe reurex simprr vex f1oen ficardid ensymg mpd entr syl2anr com ficardom
      simprl ad2antlr onomeneq mpbid isoeq4 expr eximdv rexlimdva cid cres ccnv
      wb cvv fvex word cardon eloni ordwe id isocnv isotr weniso syl3anc coeq2d
      mp2b coass adantr f1ococnv2 coeq1d syl5eqr eqtr3d f1of fcoi1 3eqtr3d gen2
      wf fcoi2 isoeq1 eu4 sylanbrc ) ABUAZAEFZSZAGUBZAHBCUCZIZCUDZXLXJAHBDUCZIZ
      SZCDUEUFZDUGCUGZXLCUHXIXNAHBXKIZCUDZDJUIZXMXIXTDJUJZYAXIXHABUOYBXGXHUKABU
      LDAEBCUMUNXTDJUPKXIXTXMDJXIXNJFZSXSXLCXIYCXSXLXIYCXSSZSZXSXLXIYCXSUQYEXNX
      JLZXSXLVRYEXNXJMNZYFYDXNAMNZAXJMNZYGXIXSYHYCXSXNAXKOYHXNAHBXKPXNAXKDURUSK
      QXHYIXGXHXJAMNYIAUTXJAEVAVBQXNAXJVCVDYEYCXJVEFZYGYFVRXIYCXSVGXHYJXGYDAVFV
      HXNXJVIUNVJXNAXJHBXKVKKVJVLVMVNVBXRXIXQCDXPXKVOXJVPZRZVOAVPZXNRZXKXNXPXKX
      KVQZXNRZRZYLYNXPYPYKXKXPXJVSFZXJHUOZXJXJHHYPIZYPYKLYRXPAGVTTYSXPXJJFXJWAY
      SAWBXJWCXJWDWKTXOXOAXJBHYOIYTXLXOWEXJAHBXKWFXJAXJHBHYOXNWGVDXJHYPVSWHWIWJ
      XPYQXKYORZXNRYNXKYOXNWLXPUUAYMXNXPXJAXKOZUUAYMLXLUUBXOXJAHBXKPZWMXJAXKWNK
      WOWPWQXPXJAXKXBZYLXKLXLUUDXOXLUUBUUDUUCXJAXKWRKWMXJAXKWSKXPXJAXNXBZYNXNLX
      OUUEXLXOXJAXNOUUEXJAHBXNPXJAXNWRKQXJAXNXCKWTXATXLXOCDXJAHBXNXKXDXEXF $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d A a b c d e f g h i j k $.  $d B a b c d e f g h i j k $.

    $( Countability of a countable union of finite sets with an strict (not
       globally well) order fulfilling the choice role.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    iunfictbso $p |- ( ( A ~<_ om /\ A C_ Fin /\ B Or U. A ) ->
        U. A ~<_ om ) $=
      ( va vf vg vh vc com cdom wbr c0 wceq cv wcel wa syl cfv cep wiso syl2anc
      vb vd ve vi vj cfn wss cuni wor w3a 0dom breq1 mpbiri a1d wne wex wfo cvv
      wi n0 csdm omex a1i ne0i unieq uni0 syl6eq necon3i adantl simpl1 brrelexi
      reldom 0sdomg 3syl mpbird fodomr syl3anc cxp cen con0 wrex ccrd cio cmpt2
      wb cif omelon onenon ax-mp xpnum mp2an wf wral simplrr fof simprl ffvelrn
      co adantr elssuni wf1o weu cab simpll3 soss sylc simpll2 sseldd finnisoeu
      iotacl iotaex isoeq1 cbvabv elab2 biimpi isof1o sylan ad2antrr ralrimivva
      f1of wn ifclda eqid fmpt2 sylib wel eluni simprr foelrn ccnv simprrl word
      ordom ficardom fveq2 eqidd ifbieq12d expr mpd exlimdv ordelss sylancr weq
      f1ocnv simprll simprrr eleqtrd fveq2d eleq2d isoeq4 isoeq5 bitrd iotabidv
      fveq1d eleq1 fvex vex ifex ovmpt2 iftrue f1ocnvfv2 3eqtrrd rcla4eov exp3a
      rexlimdv syl5bi ralrimiv foov sylanbrc xpex fodomnum mpsyl xpomen domentr
      ex sylancl expcom exlimiv sylbi pm2.61ine ) AHIJZAUFUGZAUHZBUIZUJZUWCHIJZ
      USZUWCKUWCKLZUWFUWEUWHUWFKHIJHUKUWCKHIULUMUNUWCKUOZCMZUWCNZCUPUWGCUWCUTUW
      KUWGCUWEUWKUWFUWEUWKOZHAUAMZUQZUAUPZUWFUWLHURNZKAVAJZUWAUWOUWPUWLVBVCUWLU
      WQAKUOZUWKUWRUWEUWKUWIUWRUWCUWJVDAKUWCKAKLUWCKUHKAKVEVFVGVHPVIUWLUWAAURNU
      WQUWRWEUWAUWBUWDUWKVJZAHIVLVKAURVMVNVOUWSHAURUAVPVQUWLUWNUWFUAUWEUWKUWNUW
      FUWEUWKUWNOZOZUWCHHVRZIJZUXBHVSJUWFUWJUXBVSJCVTWAZUXAUXBUWCDEHHEMZDMZUWMQ
      ZWBQZNZUXEUXHUXGRBFMZSZFWCZQZUWJWFZWDZUQZUXCUWJHVSJCVTWAZUXQUXDHVTNUXQWGC
      HWHWIZUXRCHHWJWKUXAUXBUWCUXOWLZGMZUBMUCMUXOWRLUCHWAUBHWAZGUWCWMUXPUXAUXNU
      WCNZEHWMDHWMUXSUXAUYBDEHHUXAUXFHNZUXEHNZOZOZUXIUXMUWJUWCUYFUXIOZUXGUWCUXM
      UYGUXGANZUXGUWCUGZUYFUYHUXIUYFHAUWMWLZUYCUYHUYFUWNUYJUWEUWKUWNUYEWNHAUWMW
      OZPUXAUYCUYDWPHAUXFUWMWQTZWSUXGAWTZPUYFUXHUXGUXLWLZUXIUXMUXGNUYFUXHUXGRBU
      XLSZUXHUXGUXLXAUYNUYFUXKFXBZUXLUXKFXCZNZUYOUYFUXGBUIZUXGUFNUYPUYFUYIUWDUY
      SUYFUYHUYIUYLUYMPUWAUWBUWDUWTUYEXDUXGUWCBXEXFUYFAUFUXGUWAUWBUWDUWTUYEXGUY
      LXHUXGBFXITUXKFXJUYRUYOUXHUXGRBUWJSZUYOCUXLUYQUXKFXKUXHUXGRBUXLUWJXLUXKUY
      TFCUXHUXGRBUWJUXJXLXMXNXOVNUXHUXGRBUXLXPUXHUXGUXLXTVNUXHUXGUXEUXLWQXQXHUX
      AUWKUYEUXIYAUWEUWKUWNWPXRYBXSDEHHUXNUWCUXOUXOYCZYDYEUXAUYAGUWCUXTUWCNGUDY
      FZUDMZANZOZUDUPUXAUYAUDUXTAYGUXAVUEUYAUDUXAVUEUYAUXAVUEOZVUCUEMZUWMQZLZUE
      HWAZUYAVUFUWNVUDVUJUWEUWKUWNVUEWNUXAVUBVUDYHUEHAVUCUWMYITVUFVUIUYAUEHVUFV
      UGHNZVUIUYAUXAVUEVUKVUIOZUYAUXAVUEVULOZOZVUKUXTVUHWBQZVUHRBUXJSZFWCZYJZQZ
      HNZUXTVUGVUSUXOWRZLUYAUXAVUEVUKVUIYKZVUNVUOHVUSVUNHYLVUOHNZVUOHUGYMVUNVUH
      UFNZVVCVUNAUFVUHUWAUWBUWDUWTVUMXGVUNUYJVUKVUHANZVUNUWNUYJUWEUWKUWNVUMWNUY
      KPVVBHAVUGUWMWQTZXHZVUHYNPHVUOUUAUUBVUNVUHVUOVURWLZUXTVUHNZVUSVUONZVUNVUO
      VUHVUQXAZVUHVUOVURXAVVHVUNVUOVUHRBVUQSZVVKVUNVUPFXBZVUQVUPFXCZNZVVLVUNVUH
      BUIZVVDVVMVUNVUHUWCUGZUWDVVPVUNVVEVVQVVFVUHAWTPUWAUWBUWDUWTVUMXDVUHUWCBXE
      XFVVGVUHBFXITVUPFXJVVOVVLVUOVUHRBUWJSZVVLCVUQVVNVUPFXKVUOVUHRBVUQUWJXLVUP
      VVRFCVUOVUHRBUWJUXJXLXMXNXOVNVUOVUHRBVUQXPPZVUOVUHVUQUUDVUHVUOVURXTVNVUNU
      XTVUCVUHUXAVUBVUDVULUUEUXAVUEVUKVUIUUFUUGZVUHVUOUXTVURWQTZXHZVUNVVAVVJVUS
      VUQQZUWJWFZVWCUXTVUNVUKVUTVVAVWDLVVBVWBDEVUGVUSHHUXNVWDUXOUXEVUONZUXEVUQQ
      ZUWJWFDUEUUCZUXIVWEUXMUWJVWFUWJVWGUXHVUOUXEVWGUXGVUHWBUXFVUGUWMYOZUUHZUUI
      VWGUXEUXLVUQVWGUXKVUPFVWGUXKVUOUXGRBUXJSZVUPVWGUXHVUOLUXKVWJWEVWIUXHUXGVU
      ORBUXJUUJPVWGUXGVUHLVWJVUPWEVWHVUOUXGVUHRBUXJUUKPUULUUMUUNVWGUWJYPYQUXEVU
      SLZVWEVVJVWFUWJVWCUWJUXEVUSVUOUUOUXEVUSVUQYOVWKUWJYPYQVUAVVJVWCUWJVUSVUQU
      UPCUUQUURUUSTVUNVVJVWDVWCLVWAVVJVWCUWJUUTPVUNVVKVVIVWCUXTLVVSVVTVUOVUHUXT
      VUQUVATUVBUBUCHHVUGVUSUXTUXOUVCVQYRUVDUVEYSUVOYTUVFUVGUBUCGHHUWCUXOUVHUVI
      CUXBUWCUXOHHVBVBUVJUVKUVLUVMUWCUXBHUVNUVPYRYTYSUVQUVRUVSUVT $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d A a b c d e f g h i $.
    aannenlem.a $e |- H = ( a e. NN0 |-> { b e. CC | E. c e.
        { d e. ( Poly ` ZZ ) | ( d =/= 0p /\ ( deg ` d ) <_ a /\
            A. e e. NN0 ( abs ` ( ( coeff ` d ) ` e ) ) <_ a ) }
      ( c ` b ) = 0 } ) $.
    $( Lemma for ~ aannen . $)
    aannenlem1 $p |- ( A e. NN0 -> ( H ` A ) e. Fin ) $=
      ( cn0 wcel cfv cc0 wceq cle wbr ccoe cz cc cfn wa c0p cdgr cabs wral cply
      wne w3a crab wrex breq2 ralbidv 3anbi23d rabbidv rexeqdv cnex rabex fvmpt
      cv ciun iunrab cneg cfz co cmap cdom fzfi mapfi mp2an a1i fvex cres neeq1
      cvv weq fveq2 breq1d fveq1d fveq2d 3anbi123d elrab simp3 anim2i sylbi wss
      wf wfn crn 0z coef2 mpan2 ad2antrl ffn syl cr wb adantl ffvelrn sylan zre
      eqid nn0re ad2antrr absle syl2anc nn0z znegcl elfz bitr4d biimpd ralimdva
      syl3anc impr fnfvrnss df-f elfznn0 ssriv fssres sylancl ovex elmap sylibr
      sylanbrc syl5 simp2 plyf 3syl simplrl cmul csu simplrr adantr fvres 3expa
      ex cuz dgrcl eluz mpbird coeid3 eqeltrd simplll cexp 3eqtr3d oveq1d simpr
      simp1ll simpllr simp1rl 3eqtr4d eqfnfvd expr reseq1d impbid1 expcom dom2d
      sumeq2dv syl2ani mpi domfi simp1 wi ccnv csn cima fniniseg syl6rbbr eqrdv
      eqeq1d chash fta1 simpld ralrimiv iunfi syl5eqelr ) AIJZACKEURZFURZKZLMZF
      GURZUAUFZUVTUBKZANOZBURZUVTPKZKZUCKZANOZBIUDZUGZGQUEKZUHZUIZERUHZSDAUVSFU
      WAUWBDURZNOZUWGUWONOZBIUDZUGZGUWKUHZUIZERUHUWNICUWOAMZUXAUWMERUXBUVSFUWTU
      WLUXBUWSUWJGUWKUXBUWPUWCUWRUWIUWAUWOAUWBNUJUXBUWQUWHBIUWOAUWGNUJUKULUMUNU
      MHUWMERUOUPUQUVOUWNFUWLUVSERUHZUSZSUVSFEUWLRUTUVOUWLSJZUXCSJZFUWLUDUXDSJU
      VOAVAZAVBVCZLAVBVCZVDVCZSJZUWLUXJVEOZUXEUXKUVOUXHSJUXISJUXKUXGAVFLAVFUXHU
      XIVGVHVIUVOUWLVMJUXLUWJGUWKQUEVJUPUVODEUWLUXJUWOPKZUXIVKZUVPPKZUXIVKZVMUW
      OUWLJZUWOUWKJZUWDUXMKZUCKZANOZBIUDZTZUVOUXNUXJJZUXQUXRUWOUAUFZUWOUBKZANOZ
      UYBUGZTZUYCUWJUYHGUWOUWKGDVNZUWAUYEUWCUYGUWIUYBUVTUWOUAVLUYJUWBUYFANUVTUW
      OUBVOVPUYJUWHUYABIUYJUWGUXTANUYJUWFUXSUCUYJUWDUWEUXMUVTUWOPVOVQVRVPUKVSVT
      ZUYHUYBUXRUYEUYGUYBWAWBWCUVOUYCUYDUVOUYCTZUXIUXHUXNWEZUYDUYLIUXHUXMWEZUXI
      IWDUYMUYLUXMIWFZUXMWGUXHWDZUYNUYLIQUXMWEZUYOUXRUYQUVOUYBUXRLQJZUYQWHUXMQU
      WOUXMWTZWIWJZWKIQUXMWLWMZUYLUYOUXSUXHJZBIUDZUYPVUAUVOUXRUYBVUCUVOUXRTZUYA
      VUBBIVUDUWDIJZTZUYAVUBVUFUYAUXGUXSNOUXSANOTZVUBVUFUXSWNJZAWNJZUYAVUGWOVUF
      UXSQJZVUHVUDUYQVUEVUJUXRUYQUVOUYTWPIQUWDUXMWQWRZUXSWSWMUVOVUIUXRVUEAXAXBU
      XSAXCXDVUFVUJUXGQJZAQJZVUBVUGWOVUKVUFVUMVULUVOVUMUXRVUEAXEZXBZAXFWMVUOUXS
      UXGAXGXKXHXIXJXLBIUXHUXMXMXDIUXHUXMXNYBDUXIIUWOAXOXPIUXHUXIUXMXQXRUXHUXIU
      XNUXGAVBXSLAVBXSXTYAYNYCUXQUVOUXRUYGTZUVPUWKJZUVPUBKZANOZTZUXNUXPMZDEVNZW
      OZUVPUWLJZUXQUYIVUPUYKUYHUYGUXRUYEUYGUYBYDWBWCVVDVUQUVPUAUFZVUSUWDUXOKZUC
      KZANOZBIUDZUGZTVUTUWJVVJGUVPUWKGEVNZUWAVVEUWCVUSUWIVVIUVTUVPUAVLVVKUWBVUR
      ANUVTUVPUBVOVPVVKUWHVVHBIVVKUWGVVGANVVKUWFVVFUCVVKUWDUWEUXOUVTUVPPVOVQVRV
      PUKVSVTVVJVUSVUQVVEVUSVVIYDWBWCVUPVUTTZUVOVVCVVLUVOTVVAVVBVVLUVOVVAVVBVVL
      UVOVVATZTZFRUWOUVPVVNUXRRRUWOWEUWORWFUXRUYGVUTVVMUUAQUWOYERRUWOWLYFVVNVUQ
      RRUVPWEUVPRWFVUPVUQVUSVVMYGQUVPYERRUVPWLYFVVNUVQRJZTZUXIUVTUXMKZUVQUVTUUB
      VCZYHVCZGYIZUXIUVTUXOKZVVRYHVCZGYIZUVQUWOKZUVQUVPKZVVPUXIVVSVWBGVVPUVTUXI
      JZTZVVQVWAVVRYHVWGUVTUXNKZUVTUXPKZVVQVWAVWGUVTUXNUXPVVPVVAVWFVVLUVOVVAVVO
      YJYKVQVWFVWHVVQMVVPUVTUXIUXMYLWPVWFVWIVWAMVVPUVTUXIUXOYLWPUUCUUDUUPVVPUXR
      AUYFYOKJZVVOVWDVVTMVVLVVMVVOUXRUXRUYGVUTVVMVVOUUFYMZVVPVWJUYGVVNUYGVVOUXR
      UYGVUTVVMUUGYKVVPUYFQJZVUMVWJUYGWOVVPUXRUYFIJVWLVWKQUWOYPUYFXEYFVVPUVOVUM
      VVLUVOVVAVVOYGVUNWMZUYFAYQXDYRVVNVVOUUEZUXMQGUWOAUYFUVQUYSUYFWTYSXKVVPVUQ
      AVURYOKJZVVOVWEVWCMVVLVVMVVOVUQVUQVUSVUPVVMVVOUUHYMZVVPVWOVUSVVNVUSVVOVUP
      VUQVUSVVMYJYKVVPVURQJZVUMVWOVUSWOVVPVUQVURIJVWQVWPQUVPYPVURXEYFVWMVURAYQX
      DYRVWNUXOQGUVPAVURUVQUXOWTVURWTYSXKUUIUUJUUKVVBUXMUXOUXIUWOUVPPVOUULUUMUU
      NUUQUUOUURUXJUWLUUSXDUVOUXFFUWLUVQUWLJZUVQUWKJZUVQUAUFZTZUVOUXFVWRVWSVWTU
      VQUBKZANOZUWDUVQPKZKZUCKZANOZBIUDZUGZTVXAUWJVXIGUVQUWKGFVNZUWAVWTUWCVXCUW
      IVXHUVTUVQUAVLVXJUWBVXBANUVTUVQUBVOVPVXJUWHVXGBIVXJUWGVXFANVXJUWFVXEUCVXJ
      UWDUWEVXDUVTUVQPVOVQVRVPUKVSVTVXIVWTVWSVWTVXCVXHUUTWBWCVXAUXFUVAUVOVXAUXC
      UVQUVBLUVCUVDZSVXADUXCVXKVXAUWOVXKJZUWORJUWOUVQKZLMZTZUWOUXCJVXAUVQRWFZUY
      RVXLVXOWOVWSVXPVWTVWSRRUVQWEVXPQUVQYERRUVQWLWMYKWHRLUWOUVQQUVEXRUVSVXNEUW
      OREDVNUVRVXMLUVPUWOUVQVOUVHVTUVFUVGVXAVXKSJVXKUVIKVXBNOVXKQUVQVXKWTUVJUVK
      YTVIYCUVLFUWLUXCUVMXDUVNYT $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aannen . $)
    aannenlem2 $p |- AA = U. ran H $=
      ( vf vg vh cv cfv cc0 wceq cle wbr cn0 wrex wcel wa caa c0p wne cdgr ccoe
      vi cabs wral w3a cz cply crab cc cab cuni crn csn cdif wel wex wi cpr cfz
      cun cxr clt csup simp3 eldifi adantr 3adant2 eldifsn simprbi wss 0nn0 a1i
      co dgrcl syl elexi fvex prss biimpi syl2anc ssrab2 unssd cr nn0ssre sstri
      ressxr syl6ss prid2 elun1 ax-mp supxrub sylancl fveq2 abs0 prid1 syl6eqel
      syl6eq adantl wf 0z eqid coef2 sylan nn0abscl simplr ad2antrr simpr dgrub
      ffvelrn syl3anc cvv wb elfz2nn0 syl3anbrc weq fveq2d eqeq2d rcla4ev eqeq1
      rexbidv elrab sylanbrc elun2 pm2.61dane ralrimiva breq1d 3anbi123d eqeq1d
      ralbidv cfn mp2an breq2 rabbidv eleq2 impcom anim2i neeq1 fveq1d simp2 c0
      3jca fveq1 prfi fzfi abrexfi cin dfrab2 eqsstri ssfi unfi ne0i wor xrltso
      inss1 fisup2g mpan sseldd eqidd 3anbi23d rexeqdv cnex anbi12d cla4ev 3exp
      rabex rexlimiv simp1 3imtr4i ssriv ssrexv cbvrexv syl6ib syl6bi rexlimivw
      sylbi exlimiv impbii elaa eluniab 3bitr4i eqriv rnmpt unieqi eqtr4i ) UAH
      KZDKZEKZLZMNZEFKZUBUCZUWNUDLZCKZOPZAKZUWNUELZLZUGLZUWQOPZAQUHZUIZFUJUKLZU
      LZRZDUMULZNZCQRZHUNZUOZBUPZUOIUAUXMIKZUMSZUXOJKZLZMNZJUXFUBUQZURZRZTZIHUS
      ZUXKTZHUTZUXOUASUXOUXMSUYCUYFUYBUXPUYFUXSUXPUYFVAJUYAUXQUYASZUXSUXPUYFUYG
      UXSUXPUIZUXOUWMEUWOUWPMUXQUDLZVBZUXOUFKZUXQUELZLZUGLZNZUFMUYIVCVQZRZIQULZ
      VDZVEVFVGZOPZUXBUYTOPZAQUHZUIZFUXFULZRZDUMULZSZVUGUXINZCQRZUYFUYHUXPUXOUW
      KLZMNZEVUERZVUHUYGUXSUXPVHUYHUXQVUESZUXSVUMUYHUXQUXFSZUXQUBUCZUYIUYTOPZUW
      SUYLLZUGLZUYTOPZAQUHZUIZVUNUYGUXPVUOUXSUYGVUOUXPUXQUXFUXTVIVJZVKUYGUXPVVB
      UXSUYGUXPTZVUPVUQVVAUYGVUPUXPUYGVUOVUPUXQUXFUBVLZVMVJVVDUYSVEVNZUYIUYSSZV
      UQVVDUYSQVEVVDUYJUYRQVVDMQSZUYIQSZUYJQVNZVVHVVDVOVPVVDVUOVVIVVCUJUXQVRVSZ
      VVHVVITVVJMUYIQMQVOVTZUXQUDWAZWBWCWDUYRQVNVVDUYQIQWEVPWFZQWGVEWHWJWIWKZUY
      IUYJSVVGMUYIVVMWLUYIUYJUYRWMWNZUYSUYIWOWPVVDVUTAQVVDUWSQSZTZVVFVUSUYSSZVU
      TVVDVVFVVQVVOVJVVRVVSVURMVURMNZVVSVVRVVTVUSMUYSVVTVUSMUGLMVURMUGWQWRXAMUY
      JSMUYSSMUYIVVLWSMUYJUYRWMWNWTXBVVRVURMUCZTZVUSUYRSZVVSVWBVUSQSZVUSUYNNZUF
      UYPRZVWCVVRVWDVWAVVRVURUJSZVWDVVDQUJUYLXCZVVQVWGVVDVUOMUJSVWHVVCXDUYLUJUX
      QUYLXEZXFWPQUJUWSUYLXMXGVURXHVSVJVWBUWSUYPSZVUSVUSNZVWFVWBVVQVVIUWSUYIOPZ
      VWJVVDVVQVWAXIZVVDVVIVVQVWAVVKXJVWBVUOVVQVWAVWLVVDVUOVVQVWAVVCXJVWMVVRVWA
      XKUYLUJUXQUWSUYIVWIUYIXEXLXNUYIXOSVWJVVQVVIVWLUIXPVVMXOUWSUYIXQWNXRVUSXEV
      WEVWKUFUWSUYPUFAXSZUYNVUSVUSVWNUYMVURUGUYKUWSUYLWQXTYAYBWPUYQVWFIVUSQUXOV
      USNUYOVWEUFUYPUXOVUSUYNYCYDYEYFVUSUYRUYJYGVSYHUYSVUSWOWDYIUUEVKVUDVVBFUXQ
      UXFFJXSZUWOVUPVUAVUQVUCVVAUWNUXQUBUUAZVWOUWPUYIUYTOUWNUXQUDWQZYJVWOVUBVUT
      AQVWOUXBVUSUYTOVWOUXAVURUGVWOUWSUWTUYLUWNUXQUEWQUUBXTZYJYMYKYEYFUYGUXSUXP
      UUCVULUXSEUXQVUEEJXSVUKUXRMUXOUWKUXQUUFYLZYBWDVUFVUMDUXOUMDIXSZUWMVULEVUE
      VWTUWLVUKMUWJUXOUWKWQYLZYDYEYFUYHUYTQSZVUGVUGNZVUJUYGUXPVXBUXSVVDUYSQUYTV
      VNVVDUYSYNSZUYSUUDUCZVVFUYTUYSSZVXDVVDUYJYNSUYRYNSZVXDMUYIUUGUYQIUNZYNSZU
      YRVXHVNVXGUYPYNSVXIMUYIUUHUFIUYPUYNUUIWNUYRVXHQUUJVXHUYQIQUUKVXHQUURUULVX
      HUYRUUMYOUYJUYRUUNYOVPVXEVVDVVGVXEVVPUYSUYIUUOWNVPVVOVEVFUUPVXDVXEVVFUIVX
      FUUQVEUYSVFUUSUUTXNUVAVKUYHVUGUVBVUIVXCCUYTQUWQUYTNZUXIVUGVUGVXJUXHVUFDUM
      VXJUWMEUXGVUEVXJUXEVUDFUXFVXJUWRVUAUXDVUCUWOUWQUYTUWPOYPVXJUXCVUBAQUWQUYT
      UXBOYPYMUVCYQUVDYQYAYBWDUYEVUHVUJTHVUGVUFDUMUVEUVIUWIVUGNZUYDVUHUXKVUJUWI
      VUGUXOYRVXKUXJVUICQUWIVUGUXIYCYDUVFUVGWDUVHUVJYSUYEUYCHUXKUYDUYCUXJUYDUYC
      VACQUXJUYDUXOUXISZUYCUWIUXIUXOYRVXLUXPVULEUXGRZTUYCUXHVXMDUXOUMVWTUWMVULE
      UXGVXAYDYEVXMUYBUXPUXGUYAVNZVXMUYBVAJUXGUYAVUOVUPUYIUWQOPZVUSUWQOPZAQUHZU
      IZTVUOVUPTUXQUXGSUYGVXRVUPVUOVUPVXOVXQUVKYTUXEVXRFUXQUXFVWOUWOVUPUWRVXOUX
      DVXQVWPVWOUWPUYIUWQOVWQYJVWOUXCVXPAQVWOUXBVUSUWQOVWRYJYMYKYEVVEUVLUVMVXNV
      XMVULEUYARUYBVULEUXGUYAUVNVULUXSEJUYAVWSUVOUVPWNYTUVSUVQUVRYSUVTUWAUXOJUW
      BUXKHUXOUWCUWDUWEUXNUXLCHQUXIBGUWFUWGUWH $.
      $( [16-Nov-2014] $)

    $d H f g $.
    $( Lemma for ~ aannen . $)
    $( The algebraic numbers are countable. $)
    aannenlem3 $p |- AA ~~ NN $=
      ( vf caa cn cdom wbr cen com cc cv cfn cn0 wcel cfv wor wex cnso crn cuni
      vg aannenlem2 wss con0 wrex omelon omex nn0ennn nnenom entri ensymi breq1
      wfo rcla4ev wfn cc0 wceq c0p wne cdgr cle ccoe cabs wral w3a cz cply crab
      mp2an cvv fnmpt cnex rabex a1i mprg dffn4 mpbi nn0ex fodomnum mp2 domentr
      wb fvelrnb ax-mp aannenlem1 eleq1 syl5ibcom rexlimiv sylbi ssriv eqsstr3i
      wi aasscn soss iunfictbso syl3anc syl5eqbr exlimiv nnex nnssq qssaa sstri
      cq ssdomg sbth ) IJKLZJIKLZIJMLINKLZNJMLXKOHPZUAZHUBXMHUCXOXMHXOIBUDZUEZN
      KABCDEFGUGZXOXPNKLZXPQUHZXQXNUAZXQNKLXSXOXPRKLZRNMLXSCPZRMLZCUIUJZRXPBURZ
      YBNUISNRMLZYEUKRNULRJNUMUNUOZUPYDYGCNUIYCNRMUQUSVNBRUTZYFDPEPTVAVBEFPZVCV
      DYJVETYCVFLAPYJVGTTVHTYCVFLARVIVJFVKVLTVMUJZDOVMZVOSZYICRCRYLBVOGVPYMYCRS
      YKDOVQVRVSVTZRBWAWBCRXPBWCWDWEYHXPRNWFVNVSXTXOHXPQXNXPSZUFPZBTZXNVBZUFRUJ
      ZXNQSZYIYOYSWGYNUFRXNBWHWIYRYTUFRYPRSYQQSYRYTYPABCDEFGWJYQXNQWKWLWMWNWOVS
      XQOUHXOYAWQXQIOXRWRWPXQOXNWSWIXPXNWTXAXBXCWIJNULUNUPINJWFVNJVOSJIUHXLXDJX
      HIXEXFXGJIVOXIWEIJXJVN $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d a b c d e $.
    $( The algebraic numbers are countable.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    aannen $p |- AA ~~ NN $=
      ( ve va vb vc vd cn0 cfv cc0 wceq c0p wne cdgr cle wbr ccoe cabs wral w3a
      cv crab cz cply wrex cc cmpt eqid aannenlem3 ) ABFCSDSGHIDESZJKUHLGBSZMNA
      SUHOGGPGUIMNAFQREUAUBGTUCCUDTUEZBCDEUJUFUG $.
      $( [16-Nov-2014] $)
  $}

