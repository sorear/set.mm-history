$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Alternating congruential equations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A wff like that in this theorem will be known as an "alternating
     congruence".  A special symbol might be considered if more uses come up.
     They have many of the same properties as normal congruences, starting with
     reflexivity.

     JonesMatijasevic uses "a &#8801; &#xB1; b (mod c)" for this construction.
     The disjunction of divisibility constraints seems to adequately capture
     the concept, but it's rather verbose and somewhat inelegant.  Use of an
     explicit equivalence relation might also work.  (Contributed by Stefan
     O'Rear, 2-Oct-2014.) $)
  acongid $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( A || ( B - B ) \/ A || ( B - -u B
      ) ) ) $=
    ( cz wcel wa cmin co cdivides wbr cneg congid orcd ) ACDBCDEABBFGHIABBJFGHI
    ABKL $.
    $( [2-Oct-2014] $)

  $( Symmetry of alternating congruence.  (Contributed by Stefan O'Rear,
     2-Oct-2014.) $)
  acongsym $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) /\
          ( A || ( B - C ) \/ A || ( B - -u C ) ) ) -> ( A || ( C - B ) \/ A ||
      ( C - -u B ) ) ) $=
    ( cz wcel w3a cmin co cdivides wbr cneg wo wi congsym wceq zcn 3ad2ant2 syl
    wa cc exp32 3impia negneg oveq1d negcl neg2sub syl2anc eqtr3d breq2d biimpd
    3ad2ant3 orim12d imp ) ADEZBDEZCDEZFZABCGHIJZABCKZGHZIJZLACBGHIJZACBKZGHZIJ
    ZLUQURVBVAVEUNUOUPURVBMUNUOSUPURVBABCNUAUBUQVAVEUQUTVDAIUQVCKZUSGHZUTVDUQVF
    BUSGUQBTEZVFBOUOUNVHUPBPZQBUCRUDUQVCTEZCTEZVGVDOUOUNVJUPUOVHVJVIBUERQUPUNVK
    UOCPUKVCCUFUGUHUIUJULUM $.
    $( [2-Oct-2014] $)

  $( Negate right side of alternating congruence.  Makes essential use of the
     "alternating" part.  (Contributed by Stefan O'Rear, 3-Oct-2014.) $)
  acongneg2 $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) /\
          ( A || ( B - -u C ) \/ A || ( B - -u -u C ) ) ) -> ( A || ( B - C )
      \/ A || ( B - -u C ) ) ) $=
    ( cz wcel w3a cneg cmin co cdivides wbr wo wa cc zcn 3ad2ant3 negneg oveq2d
    wceq syl breq2d biimpd orim2d imp orcomd ) ADEZBDEZCDEZFZABCGZHIJKZABUJGZHI
    ZJKZLZMUKABCHIZJKZUIUOUKUQLUIUNUQUKUIUNUQUIUMUPAJUIULCBHUICNEZULCSUHUFURUGC
    OPCQTRUAUBUCUDUE $.
    $( [3-Oct-2014] $)

  $( Transitivity of alternating congruence.  (Contributed by Stefan O'Rear,
     2-Oct-2014.) $)
  acongtr $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. ZZ /\ D e. ZZ ) /\
          ( ( A || ( B - C ) \/ A || ( B - -u C ) ) /\ ( A || ( C - D ) \/ A ||
      ( C - -u D ) ) ) ) -> ( A || ( B - D ) \/ A || ( B - -u D ) ) ) $=
    ( cz wcel wa cmin co cdivides wbr cneg wo congtr simpll ad2antlr simpr wceq
    ex adantl 3expa znegcl anim12i simplll simplrl simplrr congsym syl22anc zcn
    orcd cc adantr neg2sub syl2anc eqcomd breq2d sylibd anim2d imp syl3anc olcd
    anim2i anim1i simpl an42s syl12anc negneg syl oveq2d eqtr3d ccased 3impia )
    AEFZBEFZGZCEFZDEFZGZABCHIJKZABCLZHIJKZMACDHIJKZACDLZHIJKZMGABDHIJKZABWCHIJK
    ZMZVOVRGZVSWBWAWDWGWHVSWBGZWGWHWIGWEWFVOVRWIWEABCDNUAUJSWHWAWBGZWGWHWJGZWFW
    EWKVOVTEFZWCEFZGZWAAVTWCHIZJKZGZWFVOVRWJOVRWNVOWJVPWLVQWMCUBZDUBZUCZPWHWJWQ
    WHWBWPWAWHWBADCHIZJKZWPWHWBXBWHWBGVMVPVQWBXBVMVNVRWBUDVOVPVQWBUEVOVPVQWBUFW
    HWBQACDUGUHSWHXAWOAJWHWOXAVRWOXARZVOVRCUKFZDUKFZXCVPXDVQCUIULZVQXEVPDUITZCD
    UMUNTUOUPUQURUSABVTWCNUTVASWHVSWDGZWGWHXHGZWFWEXIVOVPWMGZXHWFVOVRXHOVRXJVOX
    HVQWMVPWSVBPWHXHQABCWCNUTVASWHWAWDGZWGWHXKGZWEWFXLVOWLVQGZWAAVTDHIZJKZGZWEV
    OVRXKOVRXMVOXKVPWLVQWRVCPWHXKXPWHWDXOWAWHWDAWCCHIZJKZXOWHWDXRWHWDGVMVPGZWMW
    DXRWHXSWDVMVQVNVPXSVMVQGVMVNVPGVPVMVQVDVNVPQUCVEULVRWMVOWDVQWMVPWSTPWHWDQAC
    WCUGVFSWHXQXNAJVRXQXNRVOVRWCVTLZHIZXQXNVRXTCWCHVRXDXTCRXFCVGVHVIVRXEVTUKFZY
    AXNRXGVRWNYBWTWLYBWMVTUIULVHDVTUMUNVJTUPUQURUSABVTDNUTUJSVKVL $.
    $( [2-Oct-2014] $)

  ${
    acongeq12d.1 $e |- ( ph -> B = C ) $.
    acongeq12d.2 $e |- ( ph -> D = E ) $.
    $( Substitution deduction for alternating congruence.  (Contributed by
       Stefan O'Rear, 3-Oct-2014.) $)
    acongeq12d $p |- ( ph -> ( ( A || ( B - D ) \/ A || ( B - -u D ) ) <-> ( A
        || ( C - E ) \/ A || ( C - -u E ) ) ) ) $=
      ( cmin co cdivides wbr cneg oveq12d breq2d negeqd orbi12d ) ABCEIJZKLBDFI
      JZKLBCEMZIJZKLBDFMZIJZKLARSBKACDEFIGHNOAUAUCBKACDTUBIGAEFHPNOQ $.
      $( [3-Oct-2014] $)
  $}

  ${
    $d A a b $.  $d N a b $.
    $( Every integer is alternating-congruent to some number in the first half
       of the fundamental domain.  (Contributed by Stefan O'Rear,
       2-Oct-2014.) $)
    acongrep $p |- ( ( A e. NN /\ N e. ZZ ) -> E. a e. ( 0 ... A ) ( ( 2 x. A )
        || ( a - N ) \/ ( 2 x. A ) || ( a - -u N ) ) ) $=
      ( vb wcel cz wa c2 cmin cdivides wbr cc0 sylancr syl2anc cle syl3anc wceq
      co wb cc cn cmul cv c1 cfz wrex cneg wo 2nn simpl nnmulcl congrep elfzelz
      simpr cr zre syl ad2antrl nnre ad2antrr elfzle1 anim1i 0z a1i elfz adantr
      nnz mpbird simplrr orcd weq id acongeq12d rcla4ev simplll simplrl w3a clt
      eqidd elfzel1 3ad2ant2 2z zmulcl 3ad2ant1 simp2 biimpa syl21anc simp3d wi
      elfzm11 2re remulcl ltle mpd subge0 nncn caddc 2times oveq1d pncan2 eqtrd
      anidms simp3 eqbrtrd suble mpbid jca zsubcl simplr simprr congsym dvdsadd
      syl22anc zsscn sseldi zcn ad2antlr subneg recnd subadd23 breqtrrd lecasei
      olcd exp32 rexlimdv ) AUAEZBFEZGZHAUBRZDUCZBIRJKZDLYIUDIRZUERZUFZYICUCZBI
      RJKYIYOBUGZIRJKUHZCLAUERZUFZYHYIUAEZYGYNYHHUAEYFYTUIYFYGUJHAUKMYFYGUNYIBD
      ULNYHYKYSDYMYHYJYMEZYKYSYHUUAYKGZGZYSYJAUUAYJUOEZYHYKUUAYJFEZUUDYJLYLUMZY
      JUPUQZURZYFAUOEZYGUUBAUSZUTUUCYJAOKZGZYJYREZYKYIYJYPIRJKZUHZYSUULUUMLYJOK
      ZUUKGZUUCUUPUUKUUAUUPYHYKYJLYLVAURVBUUCUUMUUQSZUUKUUCUUELFEZAFEZUURUUAUUE
      YHYKUUFURZUUSUUCVCVDZYFUUTYGUUBAVGZUTZYJLAVEPVFVHUULYKUUNYHUUAYKUUKVIVJYQ
      UUOCYJYRCDVKZYIYOYJBBUVEVLUVEBVSVMVNNUUCAYJOKZGZYIYJIRZYREZYIUVHBIRJKZYIU
      VHYPIRZJKZUHZYSUVGUVILUVHOKZUVHAOKZGZUVGYFUUAUVFUVPYFYGUUBUVFVOYHUUAYKUVF
      VPUUCUVFUNYFUUAUVFVQZUVNUVOUVQUVNYJYIOKZUVQYJYIVRKZUVRUVQUUEUUPUVSUVQUUSY
      IFEZUUAUUEUUPUVSVQZUUAYFUUSUVFYJLYLVTWAYFUUAUVTUVFYFHFEZUUTUVTWBUVCHAWCZM
      WDYFUUAUVFWEUUSUVTGUUAUWAYJLYIWJWFWGWHUVQUUDYIUOEZUVSUVRWIUUAYFUUDUVFUUGW
      AZYFUUAUWDUVFYFHUOEUUIUWDWKUUJHAWLMWDZYJYIWMNWNUVQUWDUUDUVNUVRSUWFUWEYIYJ
      WONVHUVQYIAIRZYJOKZUVOUVQUWGAYJOYFUUAUWGAQZUVFYFATEZUWIAWPUWJUWGAAWQRZAIR
      ZAUWJYIUWKAIAWRWSUWJUWLAQAAWTXBXAUQWDYFUUAUVFXCXDUVQUWDUUIUUDUWHUVOSUWFYF
      UUAUUIUVFUUJWDUWEYIAYJXEPXFXGPUUCUVIUVPSZUVFUUCUVHFEZUUSUUTUWMUUCUVTUUEUW
      NUUCUWBUUTUVTWBUVDUWCMZUVAYIYJXHNZUVBUVDUVHLAVEPVFVHUVGUVLUVJUUCUVLUVFUUC
      YIYIBYJIRZWQRZUVKJUUCYIUWQJKZYIUWRJKZUUCUVTUUEYGYKUWSUWOUVAYFYGUUBXIZYHUU
      AYKXJYIYJBXKXMUUCUVTUWQFEZUWSUWTSUWOUUCYGUUEUXBUXAUVABYJXHNYIUWQXLNXFUUCU
      VKUVHBWQRZUWRUUCUVHTEBTEZUVKUXCQUUCFTUVHXNUWPXOYGUXDYFUUBBXPXQZUVHBXRNUUC
      YITEYJTEUXDUXCUWRQUUCFTYIXNUWOXOUUCYJUUHXSUXEYIYJBXTPXAYAVFYCYQUVMCUVHYRY
      OUVHQZYIYOUVHBBUXFVLUXFBVSVMVNNYBYDYEWN $.
      $( [2-Oct-2014] $)
  $}

  $( Bound on the difference between two integers constrained to two possibly
     overlapping finite ranges.  (Contributed by Stefan O'Rear, 4-Oct-2014.) $)
  fzmaxdif $p |- ( ( ( C e. ZZ /\ A e. ( B ... C ) ) /\ ( F e. ZZ /\ D e. ( E
      ... F ) ) /\ ( C - E ) <_ ( F - B ) ) -> ( abs ` ( A - D ) ) <_ ( F - B )
      ) $=
    ( cz wcel co cmin cle wbr cr wb zre 3syl syl resubcl syl2anc syl3anc cfz wa
    w3a cabs caddc simp1r elfzelz simp2r simp2l elfzel1 absdifle elfzle2 lesub1
    mpbid cc wceq recnd nncan breqtrd elfzle1 letrd simp1l readdcl lesub2 simp3
    cfv lesubadd addcom mpbir2and ) CGHZABCUAIHZUBZFGHZDEFUAIHZUBZCEJIZFBJIZKLZ
    UCZADJIUDVFVQKLZDVQJIZAKLZADVQUEIZKLZVSAMHZDMHZVQMHZVTWBWDUBNVSVKAGHWEVJVKV
    OVRUFZABCUGAOPZVSVNDGHWFVLVMVNVRUHZDEFUGDOPZVSFMHZBMHZWGVSVMWLVLVMVNVRUIFOQ
    ZVSVKBGHWMWHABCUJBOPZFBRSZADVQUKTVSWABAVSWFWGWAMHWKWPDVQRSWOWIVSWAFVQJIZBKV
    SDFKLZWAWQKLZVSVNWRWJDEFULQVSWFWLWGWRWSNWKWNWPDFVQUMTUNVSFUOHBUOHWQBUPVSFWN
    UQVSBWOUQFBURSUSVSVKBAKLWHABCUTQVAVSACWCWIVSVJCMHZVJVKVOVRVBCOQZVSWFWGWCMHW
    KWPDVQVCSVSVKACKLWHABCULQVSCVQDUEIZWCKVSCDJIZVQKLZCXBKLZVSXCVPVQVSWTWFXCMHX
    AWKCDRSVSWTEMHZVPMHXAVSVNEGHXFWJDEFUJEOPZCERSWPVSEDKLZXCVPKLZVSVNXHWJDEFUTQ
    VSXFWFWTXHXINXGWKXAEDCVDTUNVLVOVRVEVAVSWTWFWGXDXENXAWKWPCDVQVGTUNVSVQUOHDUO
    HXBWCUPVSVQWPUQVSDWKUQVQDVHSUSVAVI $.
    $( [4-Oct-2014] $)

  $( Reflection of a finite range of integers about 0.  (Contributed by Stefan
     O'Rear, 4-Oct-2014.) $)
  fzneg $p |- ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) -> ( A e. ( B ... C ) <-> -u
      A e. ( -u C ... -u B ) ) ) $=
    ( cz wcel w3a cle wbr wa cneg cfz co ancom cr zre leneg syl2anc elfz znegcl
    wb 3ad2ant1 3ad2ant3 3ad2ant2 anbi12d syl5bb syl3an 3com23 3bitr4d ) ADEZBD
    EZCDEZFZBAGHZACGHZIZCJZAJZGHZUQBJZGHZIZABCKLEUQUPUSKLEZUOUNUMIULVAUMUNMULUN
    URUMUTULANEZCNEZUNURTUIUJVCUKAOUAZUKUIVDUJCOUBACPQULBNEZVCUMUTTUJUIVFUKBOUC
    VEBAPQUDUEABCRUIUKUJVBVATZUIUQDEUKUPDEUJUSDEVGASCSBSUQUPUSRUFUGUH $.
    $( [4-Oct-2014] $)

  $( Two numbers in the fundamental domain are alternating-congruent iff they
     are equal.  TODO: could be used to shorten ~ jm2.26 (Contributed by Stefan
     O'Rear, 4-Oct-2014.) $)
  acongeq $p |- ( ( A e. NN /\ B e. ( 0 ... A ) /\ C e. ( 0 ... A ) ) -> ( B =
      C <-> ( ( 2 x. A ) || ( B - C ) \/ ( 2 x. A ) || ( B - -u C ) ) ) ) $=
    ( wcel cc0 co wceq c2 cmin wbr cz syl2anc clt cc cr syl cle caddc ad2antrr
    c1 cn cfz w3a cmul cdivides cneg wo wa nnz 3ad2ant1 zmulcl sylancr 3ad2ant2
    2z elfzelz congid adantr oveq2 adantl breqtrd orcd cabs cfv 3ad2ant3 zsubcl
    zsscn sseldi abscl nnre 0re resubcl sylancl 2re remulcl simp2 leid fzmaxdif
    simp3 syl221anc crp nnrp ltaddrp recnd subid1 2times 3brtr4d lelttrd wb 2nn
    simpl1 nnmulcl simpl2 simpl3 simpr congabseq syl31anc mpbid cuz nnnn0 nn0uz
    cn0 syl6eleq fzm1 biimpa renegcl recn 3syl 1re znegcl abssub elfzel1 0z a1i
    zssre 1z fzneg syl3anc neg0 oveq2d eleqtrd simpll2 simp1 nnm1nn0 nn0ge0 0cn
    subid1i ax-1cn addsubass oveq1d subneg 3eqtr4rd eqbrtrd ltm1 simplr elfzle1
    subcl zre mpbird eqtr4d jaodan letri3 le0neg1 negeqd 3eqtrd fveq2d eqbrtrrd
    mpbir2and ppncan addcom eqtrd 3eqtr4d breqtrrd dvdsadd mpdan impbida ) AUAD
    ZBEAUBFZDZCUUQDZUCZBCGZHAUDFZBCIFZUEJZUVBBCUFZIFZUEJZUGUUTUVAUHZUVDUVGUVHUV
    BBBIFZUVCUEUUTUVBUVIUEJZUVAUUTUVBKDZBKDZUVJUUTHKDAKDZUVKUNUUPUURUVMUUSAUIUJ
    ZHAUKULZUURUUPUVLUUSBEAUOZUMZUVBBUPLUQUVAUVIUVCGUUTBCBIURUSUTVAUUTUVDUVAUVG
    UUTUVDUHZUVCVBVCZUVBMJZUVAUUTUVTUVDUUTUVSAEIFZUVBUUTUVCNDUVSODUUTKNUVCVFUUT
    UVLCKDZUVCKDUVQUUSUUPUWBUURCEAUOZVDZBCVELVGUVCVHPUUTAODZEODZUWAODZUUPUURUWE
    UUSAVIUJZVJAEVKVLZUUTHODUWEUVBODZVMUWHHAVNULZUUTUVMUURUVMUUSUWAUWAQJZUVSUWA
    QJUVNUUPUURUUSVOUVNUUPUURUUSVRZUUTUWGUWLUWIUWAVPPBEACEAVQVSUUTAAARFZUWAUVBM
    UUTUWEAVTDZAUWNMJUWHUUPUURUWOUUSAWAUJAAWBLUUTANDZUWAAGUUTAUWHWCZAWDPUUTUWPU
    VBUWNGUWQAWEPZWFWGZUQUVRUVBUADZUVLUWBUVDUVTUVAWHUVRHUADZUUPUWTWIUUPUURUUSUV
    DWJHAWKZULUVRUURUVLUUPUURUUSUVDWLUVPPUVRUUSUWBUUPUURUUSUVDWMUWCPUUTUVDWNUVB
    BCWOWPWQUUTUVGUHZCEATIFZUBFDZCAGZUGZUVAUUTUXGUVGUUTAEWRVCZDZUUSUXGUUTAXAUXH
    UUPUURAXADUUSAWSUJWTXBUWMUXIUUSUXGCEAXCXDLUQUXCUXEUVAUXFUXCUXEUHZBECUXJBUVE
    EUFZEUXJUVFVBVCZUVBMJZBUVEGZUXJUXLAUXDUFZIFZUVBUUTUXLODZUVGUXEUUTUVFODZUVFN
    DUXQUUTBODUVEODZUXRUUTKOBXNUVQVGZUUTCODZUXSUUTKOCXNUWDVGZCXEPBUVEVKLUVFXFUV
    FVHXGSUUTUXPODZUVGUXEUUTUWEUXOODZUYCUWHUUTUXDODZUYDUUTUWETODUYEUWHXHATVKVLU
    XDXEPAUXOVKLSUUTUWJUVGUXEUWKSUXJUXLUVEBIFVBVCZUXPQUXJBNDZUVENDUXLUYFGUXJKNB
    VFUUTUVLUVGUXEUVQSZVGUXJKNUVEVFUUTUVEKDZUVGUXEUUTUUSUWBUYIUWMUWCCXIXGSZVGBU
    VEXJLUXJEKDZUVEUXOEUBFZDUVMUUREEIFZUXPQJZUYFUXPQJUXEUYKUXCCEUXDXKUSUXJUVEUX
    OUXKUBFZUYLUXJUXEUVEUYODZUXCUXEWNUUTUXEUYPWHZUVGUXEUUTUWBUYKUXDKDZUYQUWDUYK
    UUTXLXMUUTUVMTKDUYRUVNXOATVEVLCEUXDXPXQSWQUXJUXKEUXOUBUXKEGUXJXRXMZXSXTUUTU
    VMUVGUXEUVNSUUPUURUUSUVGUXEYAZUUTUYNUVGUXEUUTEUVBTIFZUYMUXPQUUTUWTVUAXADEVU
    AQJUUTUXAUUPUWTWIUUPUURUUSYBUXBULZUVBYCVUAYDXGUYMEGUUTEYEYFXMUUTUWNTIFZAUXD
    RFZVUAUXPUUTUWPUWPTNDZVUCVUDGUWQUWQVUEUUTYGXMAATYHXQUUTUVBUWNTIUWRYIUUTUWPU
    XDNDZUXPVUDGUWQUUTUWPVUEVUFUWQYGATYPVLAUXDYJLYKZWFSUVEUXOEBEAVQVSYLUUTUXPUV
    BMJUVGUXEUUTUXPVUAUVBMVUGUUTUWJVUAUVBMJUWKUVBYMPYLSWGUXJUWTUVLUYIUVGUXMUXNW
    HUUTUWTUVGUXEVUBSUYHUYJUUTUVGUXEYNUVBBUVEWOWPWQZUXJCEUXJCEGZCEQJZECQJZUXJUY
    AUWFVUIVUJVUKUHWHUXEUYAUXCUXEUWBUYACEUXDUOCYQPUSZVJCEUUAVLUXJVUJEUVEQJZUXJE
    BUVEQUXJUUREBQJUYTBEAYOPVUHUTUXJUYAVUJVUMWHVULCUUBPYRUXEVUKUXCCEUXDYOUSUUGZ
    UUCUYSUUDVUNYSUXCUXFUHZBACVUOBAIFZVBVCZUVBMJZBAGZVUOUVSVUQUVBMVUOUVCVUPVBUX
    FUVCVUPGUXCCABIURUSUUEUUTUVTUVGUXFUWSSUUFVUOUWTUVLUVMUVBVUPUEJZVURVUSWHUUTU
    WTUVGUXFVUBSUUTUVLUVGUXFUVQSUUTUVMUVGUXFUVNSVUOVUTUVBUVBVUPRFZUEJZVUOUVBUVF
    VVAUEUUTUVGUXFYNVUOUWNVUPRFZBCRFZVVAUVFVUOVVCBARFZVVDUUTVVCVVEGUVGUXFUUTVVC
    ABRFZVVEUUTUWPUWPUYGVVCVVFGUWQUWQUUTBUXTWCZAABUUHXQUUTUWPUYGVVFVVEGUWQVVGAB
    UUILUUJSUXFVVDVVEGUXCCABRURUSYSUUTVVAVVCGUVGUXFUUTUVBUWNVUPRUWRYISUUTUVFVVD
    GZUVGUXFUUTUYGCNDVVHVVGUUTCUYBWCBCYJLSUUKUULVUOUVKVUPKDZVUTVVBWHUUTUVKUVGUX
    FUVOSUUTVVIUVGUXFUUTUVLUVMVVIUVQUVNBAVELSUVBVUPUUMLYRUVBBAWOWPWQUXCUXFWNYSY
    TUUNYTUUO $.
    $( [4-Oct-2014] $)

  $( Alternating congruence passes from a base to a dividing base.
     (Contributed by Stefan O'Rear, 4-Oct-2014.) $)
  dvdsacongtr $p |- ( ( ( A e. ZZ /\ B e. ZZ ) /\ ( C e. ZZ /\ D e. ZZ ) /\ ( D
      || A /\ ( A || ( B - C ) \/ A || ( B - -u C ) ) ) ) -> ( D || ( B - C )
      \/ D || ( B - -u C ) ) ) $=
    ( cz wcel wa cdivides wbr cmin co wo simplr simpr wi ad2antrr zsubcl dvdstr
    syl2anc syl3anc cneg simprr simpll simprl mp2and znegcl syl orim12d expimpd
    ex 3impia ) AEFZBEFZGZCEFZDEFZGZDAHIZABCJKZHIZABCUAZJKZHIZLZGDUSHIZDVBHIZLZ
    UNUQGZURVDVGVHURGZUTVEVCVFVIUTVEVIUTGZURUTVEVHURUTMVIUTNVJUPULUSEFZURUTGVEO
    VHUPURUTUNUOUPUBZPVHULURUTULUMUQUCZPVJUMUOVKVHUMURUTULUMUQMZPVHUOURUTUNUOUP
    UDZPBCQSDAUSRTUEUJVIVCVFVIVCGZURVCVFVHURVCMVIVCNVPUPULVBEFZURVCGVFOVHUPURVC
    VLPVHULURVCVMPVPUMVAEFZVQVHUMURVCVNPVPUOVRVHUOURVCVOPCUFUGBVAQSDAVBRTUEUJUH
    UIUK $.
    $( [4-Oct-2014] $)

