$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Diophantine sets 5: Arithmetic sets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d N t $.
    $( Lemma for arithmetic diophantine sets.  Convert polynomial-ness of an
       expression into a constraint suitable for ~ ralimi . $)
    rabdiophlem1 $p |- ( ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e. ( mzPoly ` ( 1
        ... N ) ) -> A. t e. ( NN0 ^m ( 1 ... N ) ) A e. ZZ ) $=
      ( cz c1 cfz co cmap cmpt cmzp cfv wcel wf wral cn0 mzpf eqid fmpt biimpri
      wss wi nn0ssz zex ovex mapss ssralv mp2b 3syl ) ADECFGZHGZBIZUIJKLUJDUKMZ
      BDLZAUJNZUMAOUIHGZNZUKUIPUNULAUJDBUKUKQRSODTUOUJTUNUPUAUBODUIUCECFUDUEUMA
      UOUJUFUGUH $.
      $( [10-Oct-2014] $)
  $}

  ${
    $d N a b c u t $.  $d M a b c u t $.  $d A a b c t $.
    rabdiophlem2.1 $e |- M = ( N + 1 ) $.
    $( Lemma for arithmetic diophantine sets.  Reuse a polynomial expression
       under a new quantifier. $)
    rabdiophlem2 $p |- ( ( N e. NN0 /\ ( u e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e.
        ( mzPoly ` ( 1 ... N ) ) ) -> ( t e. ( ZZ ^m ( 1 ... M ) ) |-> [_ ( t
        |` ( 1 ... N ) ) / u ]_ A ) e. ( mzPoly ` ( 1 ... M ) ) ) $=
      ( va vb cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv cv ax-17 wa cres csb vex
      wel hbcsb1OLD csbeq1a cbvmpt fveq1i wceq mapfzcons1cl wral mzpf eqid fmpt
      adantlr wf sylibr ad2antlr resex hbel eleq1d rcla4 csbeq1 syl2anc syl5req
      sylc fvmptg mpteq2dva cvv ovex caddc fzssp1 oveq2i syl6sseqr adantr simpr
      wss a1i mzpresrename syl3anc eqeltrd ) EIJZAKLEMNZONZCPZWDQRJZUAZBKLDMNZO
      NZABSZWDUBZCUCZPBWJWLWFRZPZWIQRZWHBWJWMWNWHWKWJJZUAZWNWLGWEAGSZCUCZPZRZWM
      WLWFXAAGHWECWTHSCJGTAHWSCGUDHGUEATUFAWSCUGUHUIWRWLWEJZWMKJZXBWMUJWCWQXCWG
      WKKDEFUKUPZWRXCCKJZAWEULZXDXEWGXGWCWQWGWEKWFUQXGWFWDUMAWEKCWFWFUNUOURUSXF
      XDAWLWEAGGWMKAGWLCWKWDBUDUTWSWLJATUFWSKJATVAASWLUJCWMKAWLCUGVBVCVGGWLWTWM
      WEKXAAWSWLCVDXAUNVHVEVFVIWHWIVJJZWDWIVRZWGWOWPJXHWHLDMVKVSWCXIWGWCWDLELVL
      NZMNWILEIVMDXJLMFVNVOVPWCWGVQBWFWDWIVTWAWB $.
      $( [10-Oct-2014] $)
  $}

  ${
    $d A a b c $.  $d N a b c t $.
    $( Diophantine set builder for nonnegativity constraints.  The first
       builder which uses a witness variable internally; an expression is
       nonnegative if there is a nonnegative integer equal to it.  (Contributed
       by Stefan O'Rear, 11-Oct-2014.) $)
    elnn0rabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e.
        ( mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A e. NN0
        } e. ( Dioph ` N ) ) $=
      ( vb va vc cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv crab cv wceq ax-17 wa
      csb wrex cdioph risset a1i rabbiia vex wel hbcsb1OLD hbeleq hbrex csbeq1a
      weq eqeq2d rexbidv cbvrab syl6eq caddc cres peano2nn0 adantr ovex nn0p1nn
      wb cn elfz1end sylib mzpproj sylancr eqid rabdiophlem2 eqrabdioph syl3anc
      cvv eqeq1 csbeq1 rexrabdioph syldan eqeltrd ) CGHZAIJCKLZMLBNWBOPHZUAZBGH
      ZAGWBMLZQZDRZAERZBUBZSZDGUCZEWFQZCUDPZWDWGWHBSZDGUCZAWFQZWMWGWQSWDWEWPAWF
      WEWPVEARWFHDBGUEUFUGUFWPWLAEDWFWHWFHZATWRETWPETWKADGWHGHATADWJADWIBEUHDEU
      IATUJUKULAEUNZWOWKDGWSBWJWHAWIBUMUOUPUQURWAWCCJUSLZFRZPZAXAWBUTZBUBZSZFGJ
      WTKLZMLQWTUDPHZWMWNHWDWTGHZFIXFMLZXBNXFOPZHZFXIXDNXJHXGWAXHWCCVAVBWDXFVOH
      WTXFHZXKJWTKVCWAXLWCWAWTVFHXLCVDWTVGVHVBFXFWTVIVJAFBWTCWTVKZVLFXBXDWTVMVN
      XEWKXBWJSDEFWTCXMWHXBWJVPWIXCSWJXDXBAWIXCBVQUOVRVSVT $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d ph y a b c $.  $d ps x a b c $.  $d ch x a b c $.  $d x y $.
    rexzrexnn0.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    rexzrexnn0.2 $e |- ( x = -u y -> ( ph <-> ch ) ) $.
    $( Rewrite a quantification over integers into a quantification over
       naturals.  (Contributed by Stefan O'Rear, 11-Oct-2014.) $)
    rexzrexnn0 $p |- ( E. x e. ZZ ph <-> E. y e. NN0 ( ps \/ ch ) ) $=
      ( cz wrex wo cn0 cv wcel wa cneg weq wb rcla4ev wceq rexlimiva cr simprbi
      elznn0 adantr simpr simplr equcom bicom 3imtr4i syl2anc ex zcn negneg syl
      cc eqcomd eqeq2d syl5ibrcom imp bicom1 3syl rcla4edv impancom orim12d mpd
      negeq r19.43 sylibr nn0z sylan nn0negz jaodan impbii ) ADHIZBCJZEKIZAVPDH
      DLZHMZANZBEKIZCEKIZJZVPVSVQKMZVQOZKMZJZWBVRWFAVRVQUAMWFVQUCUBUDVSWCVTWEWA
      VSWCVTVSWCNWCAVTVSWCUEVRAWCUFBAEVQKDEPABQEDPBAQFEDUGBAUHUIRUJUKVRWEAWAVRC
      AEWDKVRELZWDSZNVQWGOZSZACQCAQVRWHWJVRWJWHVQWDOZSVRWKVQVRVQUOMWKVQSVQULVQU
      MUNUPWHWIWKVQWGWDVFUQURUSGACUTVAVBVCVDVEBCEKVGVHTVOVNEKWGKMZBVNCWLWGHMBVN
      WGVIABDWGHFRVJWLWIHMCVNWGVKACDWIHGRVJVLTVM $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d N t $.  $d M t $.
    $( Diophantine set builder for the less or equals relation.  (Contributed
       by Stefan O'Rear, 11-Oct-2014.) $)
    lerabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e. (
        mzPoly ` ( 1 ... N ) ) /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> B ) e. (
        mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A <_ B } e.
        ( Dioph ` N ) ) $=
      ( cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv crab wral rabdiophlem1 3adant1
      w3a wa cle cmin cdioph wceq wb znn0sub ralimi r19.26 rabbi 3imtr3i syl2an
      wbr simp1 mzpsubmpt ancoms elnn0rabdioph syl2anc eqeltrd ) DEFZAGHDIJZKJZ
      BLUTMNZFZAVACLVBFZSZBCUAULZAEUTKJZOZCBUBJZEFZAVGOZDUCNZVCVDVHVKUDZUSVCBGF
      ZAVGPZCGFZAVGPZVMVDABDQACDQVNVPTZAVGPVFVJUEZAVGPVOVQTVMVRVSAVGBCUFUGVNVPA
      VGUHVFVJAVGUIUJUKRVEUSAVAVILVBFZVKVLFUSVCVDUMVCVDVTUSVDVCVTACBUTUNUORAVID
      UPUQUR $.
      $( [11-Oct-2014] $)

    $( Diophantine set builder for membership in a fixed set of upper
       integers.  (Contributed by Stefan O'Rear, 11-Oct-2014.) $)
    eluzrabdioph $p |- ( ( N e. NN0 /\ M e. ZZ /\ ( t e. ( ZZ ^m ( 1 ... N ) )
        |-> A ) e. ( mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) )
        | A e. ( ZZ>= ` M ) } e. ( Dioph ` N ) ) $=
      ( cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv w3a cuz crab cle wbr wral wceq
      cdioph wa wb rabdiophlem1 eluz ralimdv imp sylan2 rabbi sylib 3adant1 cvv
      ex ovex mzpconstmpt mpan lerabdioph syl3an2 eqeltrd ) DEFZCGFZAGHDIJZKJZB
      LVCMNZFZOBCPNFZAEVCKJZQZCBRSZAVHQZDUBNZVBVFVIVKUAZVAVBVFUCVGVJUDZAVHTZVMV
      FVBBGFZAVHTZVOABDUEVBVQVOVBVPVNAVHVBVPVNCBUFUNUGUHUIVGVJAVHUJUKULVBVAAVDC
      LVEFZVFVKVLFVCUMFVBVRHDIUOACVCUPUQACBDURUSUT $.
      $( [11-Oct-2014] $)

    $( Diophantine set builder for positivity.  (Contributed by Stefan O'Rear,
       11-Oct-2014.) $)
    elnnrabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e.
        ( mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A e. NN }
        e. ( Dioph ` N ) ) $=
      ( cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv wa cn crab cuz cdioph wb cv 1z
      elnnuz a1i rabbiia eluzrabdioph mp3an2 syl5eqel ) CDEZAFGCHIZJIBKUILMEZNB
      OEZADUIJIZPBGQMEZAULPZCRMZUKUMAULUKUMSATULEBUBUCUDUHGFEUJUNUOEUAABGCUEUFU
      G $.
      $( [11-Oct-2014] $)

    $( Diophantine set builder for the strict less than relation.  (Contributed
       by Stefan O'Rear, 11-Oct-2014.) $)
    ltrabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e. (
        mzPoly ` ( 1 ... N ) ) /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> B ) e. (
        mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A < B } e.
        ( Dioph ` N ) ) $=
      ( cn0 wcel cz c1 cfz co cmap cmpt cmzp cfv crab wral rabdiophlem1 3adant1
      w3a wa clt wbr cmin cn cdioph wceq wb znnsub ralimi r19.26 3imtr3i syl2an
      rabbi simp1 mzpsubmpt ancoms elnnrabdioph syl2anc eqeltrd ) DEFZAGHDIJZKJ
      ZBLVAMNZFZAVBCLVCFZSZBCUAUBZAEVAKJZOZCBUCJZUDFZAVHOZDUENZVDVEVIVLUFZUTVDB
      GFZAVHPZCGFZAVHPZVNVEABDQACDQVOVQTZAVHPVGVKUGZAVHPVPVRTVNVSVTAVHBCUHUIVOV
      QAVHUJVGVKAVHUMUKULRVFUTAVBVJLVCFZVLVMFUTVDVEUNVDVEWAUTVEVDWAACBVAUOUPRAV
      JDUQURUS $.
      $( [11-Oct-2014] $)

    $( Diophantine set builder for inequality.  This not quite trivial theorem
       touches on something important; Diophantine sets are not closed under
       negation, but they contain an important subclass that is, namely the
       recursive sets.  With this theorem and De Morgan's laws, all
       quantifier-free formulae can be negated.  (Contributed by Stefan O'Rear,
       11-Oct-2014.) $)
    nerabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e. (
        mzPoly ` ( 1 ... N ) ) /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> B ) e. (
        mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A =/= B }
        e. ( Dioph ` N ) ) $=
      ( cn0 wcel cz co cmap cmpt cfv crab clt wbr rabdiophlem1 wa cr zre syl2an
      wral c1 cfz cmzp w3a wo cdioph wceq wb lttri2 ralimi r19.26 rabbi 3imtr3i
      wne 3adant1 ltrabdioph 3com23 orrabdioph syl2anc eqeltrd ) DEFZAGUADUBHZI
      HZBJVBUCKZFZAVCCJVDFZUDZBCUNZAEVBIHZLZBCMNZCBMNZUEZAVILZDUFKZVEVFVJVNUGZV
      AVEBGFZAVITZCGFZAVITZVPVFABDOACDOVQVSPZAVITVHVMUHZAVITVRVTPVPWAWBAVIVQBQF
      CQFWBVSBRCRBCUISUJVQVSAVIUKVHVMAVIULUMSUOVGVKAVILVOFVLAVILVOFZVNVOFABCDUP
      VAVFVEWCACBDUPUQVKVLADURUSUT $.
      $( [11-Oct-2014] $)
  $}

  ${
    $d N a b c d t $.  $d A a b c d $.  $d B a b c d $.

    $( Divisibility is a Diophantine relation.  (Contributed by Stefan O'Rear,
       11-Oct-2014.) $)
    dvdsrabdioph $p |- ( ( N e. NN0 /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> A ) e.
        ( mzPoly ` ( 1 ... N ) ) /\ ( t e. ( ZZ ^m ( 1 ... N ) ) |-> B ) e. (
        mzPoly ` ( 1 ... N ) ) ) -> { t e. ( NN0 ^m ( 1 ... N ) ) | A || B } e.
        ( Dioph ` N ) ) $=
      ( vb va vc cn0 wcel cz c1 co cmap cmpt cfv crab cmul wceq wo ax-17 cfz cv
      cmzp w3a cdivides wbr cneg wrex cdioph wral rabdiophlem1 wa divides oveq1
      weq eqeq1d rexzrexnn0 syl6bb ralimi r19.26 3imtr3i syl2an 3adant1 csb wel
      rabbi vex hbcsb1OLD hbov hbeq hbor csbeq1a oveq2d eqeq12d orbi12d rexbidv
      wb hbrex cbvrab caddc cres simp1 peano2nn0 3ad2ant1 ovex nn0p1nn elfz1end
      cvv cn mzpproj sylancr adantr eqid rabdiophlem2 mzpmulmpt syl2anc 3adant3
      sylib 3adant2 eqrabdioph syl3anc mzpnegmpt orrabdioph negeq oveq1d csbeq1
      syl rexrabdioph syl5eqel eqeltrd ) DHIZAJKDUALZMLZBNXLUCOZIZAXMCNXNIZUDZB
      CUEUFZAHXLMLZPZEUBZBQLZCRZYAUGZBQLZCRZSZEHUHZAXSPZDUIOZXOXPXTYIRZXKXOBJIZ
      AXSUJZCJIZAXSUJZYKXPABDUKACDUKYLYNULZAXSUJXRYHVQZAXSUJYMYOULYKYPYQAXSYPXR
      FUBZBQLZCRZFJUHYHFBCUMYTYCYFFEFEUOYSYBCYRYABQUNUPYRYDRYSYECYRYDBQUNUPUQUR
      USYLYNAXSUTXRYHAXSVFVAVBVCXQYIYAAYRBVDZQLZAYRCVDZRZYDUUAQLZUUCRZSZEHUHZFX
      SPZYJYHUUHAFGXSGUBZXSIZATUUKFTYHFTUUGAEHYAHIATUUDUUFAAGGUUBUUCAGYAUUAQGEV
      EATUUJQIATZAGYRBFVGZGFVEATZVHZVIAGYRCUUMUUNVHZVJAGGUUEUUCAGYDUUAQUUJYDIAT
      UULUUOVIUUPVJVKVRAFUOZYGUUGEHUUQYCUUDYFUUFUUQYBUUBCUUCUUQBUUAYAQAYRBVLZVM
      AYRCVLZVNUUQYEUUECUUCUUQBUUAYDQUURVMUUSVNVOVPVSXQXKDKVTLZUUJOZAUUJXLWAZBV
      DZQLZAUVBCVDZRZUVAUGZUVCQLZUVERZSZGHKUUTUALZMLZPUUTUIOZIZUUIYJIXKXOXPWBXQ
      UVFGUVLPUVMIZUVIGUVLPUVMIZUVNXQUUTHIZGJUVKMLZUVDNUVKUCOZIZGUVRUVENUVSIZUV
      OXKXOUVQXPDWCWDZXKXOUVTXPXKXOULZGUVRUVANUVSIZGUVRUVCNUVSIZUVTXKUWDXOXKUVK
      WHIUUTUVKIZUWDKUUTUAWEXKUUTWIIUWFDWFUUTWGWRGUVKUUTWJWKWLZAGBUUTDUUTWMZWNZ
      GUVAUVCUVKWOWPWQXKXPUWAXOAGCUUTDUWHWNWSZGUVDUVEUUTWTXAXQUVQGUVRUVHNUVSIZU
      WAUVPUWBXKXOUWKXPUWCGUVRUVGNUVSIZUWEUWKUWCUWDUWLUWGGUVAUVKXBXGUWIGUVGUVCU
      VKWOWPWQUWJGUVHUVEUUTWTXAUVFUVIGUUTXCWPUVJUUGUVAUUAQLZUUCRZUVGUUAQLZUUCRZ
      SEFGUUTDUWHYAUVARZUUDUWNUUFUWPUWQUUBUWMUUCYAUVAUUAQUNUPUWQUUEUWOUUCUWQYDU
      VGUUAQYAUVAXDXEUPVOYRUVBRZUWNUVFUWPUVIUWRUWMUVDUUCUVEUWRUUAUVCUVAQAYRUVBB
      XFZVMAYRUVBCXFZVNUWRUWOUVHUUCUVEUWRUUAUVCUVGQUWSVMUWTVNVOXHWPXIXJ $.
      $( [11-Oct-2014] $)
  $}

