$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Properties of the canonical representation of a rational
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c numer denom $.

  $( Extend class notation to include canonical numerator function. $)
  cnumer $a class numer $.
  $( Extend class notation to include canonical denominator function. $)
  cdenom $a class denom $.

  ${
    $d x y $.
    $( The canonical numerator of a rational is the numerator of the rational's
       reduced fraction representation (no common factors, denominator
       positive). $)
    df-numer $a |- numer = ( y e. QQ |-> ( 1st ` ( iota_ x e. ( ZZ X. NN ) ( (
        ( 1st ` x ) gcd ( 2nd ` x ) ) = 1 /\ y = ( ( 1st ` x ) / ( 2nd ` x ) )
        ) ) ) ) $.
    $( The canonical denominator of a rational is the denominator of the
       rational's reduced fraction representation (no common factors,
       denominator positive). $)
    df-denom $a |- denom = ( y e. QQ |-> ( 2nd ` ( iota_ x e. ( ZZ X. NN ) ( (
        ( 1st ` x ) gcd ( 2nd ` x ) ) = 1 /\ y = ( ( 1st ` x ) / ( 2nd ` x ) )
        ) ) ) ) $.
  $}

  ${
    $d A a b c d $.  $d B a b c d $.  $d C a b c d $.  $d x y a b c d $.

    ${
      $d A x $.
      $( Value of the canonical numerator function.  (Contributed by Stefan
         O'Rear, 13-Sep-2014.) $)
      qnumval $p |- ( A e. QQ -> ( numer ` A ) = ( 1st ` ( iota_ x e. ( ZZ X.
          NN ) ( ( ( 1st ` x ) gcd ( 2nd ` x ) ) = 1 /\ A = ( ( 1st ` x ) / (
          2nd ` x ) ) ) ) ) ) $=
        ( va cv c1st cfv c2nd cgcd co c1 wceq cdiv wa cz cn cxp cq cnumer eqeq1
        crio anbi2d riotabidv fveq2d df-numer fvex fvmpt ) CBADZEFZUGGFZHIJKZCD
        ZUHUILIZKZMZANOPZTZEFUJBULKZMZAUOTZEFQRUKBKZUPUSEUTUNURAUOUTUMUQUJUKBUL
        SUAUBUCACUDUSEUEUF $.
        $( [13-Sep-2014] $)

      $( Value of the canonical denominator function.  (Contributed by Stefan
         O'Rear, 13-Sep-2014.) $)
      qdenval $p |- ( A e. QQ -> ( denom ` A ) = ( 2nd ` ( iota_ x e. ( ZZ X.
          NN ) ( ( ( 1st ` x ) gcd ( 2nd ` x ) ) = 1 /\ A = ( ( 1st ` x ) / (
          2nd ` x ) ) ) ) ) ) $=
        ( va cv c1st cfv c2nd cgcd co c1 wceq cdiv wa cz cn cxp cq cdenom eqeq1
        crio anbi2d riotabidv fveq2d df-denom fvex fvmpt ) CBADZEFZUGGFZHIJKZCD
        ZUHUILIZKZMZANOPZTZGFUJBULKZMZAUOTZGFQRUKBKZUPUSGUTUNURAUOUTUMUQUJUKBUL
        SUAUBUCACUDUSGUEUF $.
        $( [13-Sep-2014] $)
    $}

    $( Lemma for ~ qnumcl and ~ qdencl . $)
    qnumdencl $p |- ( A e. QQ -> ( ( numer ` A ) e. ZZ /\ ( denom ` A ) e. NN )
        ) $=
      ( va cq wcel cv c1st cfv c2nd cgcd co c1 wceq cdiv wa cz cn cnumer eleq1d
      cxp crio cdenom wreu qredeu riotacl syl cop elxp6 qnumval qdenval anbi12d
      biimprd adantld syl5bi mpd ) ACDZBEZFGZUPHGZIJKLAUQURMJLNZBOPSZTZUTDZAQGZ
      ODZAUAGZPDZNZUOUSBUTUBVBBAUCUSBUTUDUEVBVAVAFGZVAHGZUFLZVHODZVIPDZNZNUOVGV
      AOPUGUOVMVGVJUOVGVMUOVDVKVFVLUOVCVHOBAUHRUOVEVIPBAUIRUJUKULUMUN $.
      $( [13-Sep-2014] $)

    $( The canonical numerator of a rational is an integer.  (Contributed by
       Stefan O'Rear, 13-Sep-2014.) $)
    qnumcl $p |- ( A e. QQ -> ( numer ` A ) e. ZZ ) $=
      ( cq wcel cnumer cfv cz cdenom cn qnumdencl simpld ) ABCADEFCAGEHCAIJ $.
      $( [13-Sep-2014] $)

    $( The canonical denominator is a positive integer.  (Contributed by Stefan
       O'Rear, 13-Sep-2014.) $)
    qdencl $p |- ( A e. QQ -> ( denom ` A ) e. NN ) $=
      ( cq wcel cnumer cfv cz cdenom cn qnumdencl simprd ) ABCADEFCAGEHCAIJ $.
      $( [13-Sep-2014] $)

    $( Canonical numerator defines a function.  (Contributed by Stefan O'Rear,
       13-Sep-2014.) $)
    fnum $p |- numer : QQ --> ZZ $=
      ( vb va cv c1st cfv c2nd cgcd co c1 wceq cdiv wa cz cn cxp crio cq cnumer
      wcel wf wral df-numer fmpt biimpi qnumval qnumcl eqeltrrd mprg ) ACZDEZUI
      FEZGHIJBCZUJUKKHJLAMNOPDEZMSZQMRTZBQUNBQUAUOBQMUMRABUBUCUDULQSULREUMMAULU
      EULUFUGUH $.
      $( [13-Sep-2014] $)

    $( Canonical denominator defines a function.  (Contributed by Stefan
       O'Rear, 13-Sep-2014.) $)
    fden $p |- denom : QQ --> NN $=
      ( vb va cv c1st cfv c2nd cgcd co c1 wceq cdiv wa cz cn cxp crio cq cdenom
      wcel wf wral df-denom fmpt biimpi qdenval qdencl eqeltrrd mprg ) ACZDEZUI
      FEZGHIJBCZUJUKKHJLAMNOPFEZNSZQNRTZBQUNBQUAUOBQNUMRABUBUCUDULQSULREUMNAULU
      EULUFUGUH $.
      $( [13-Sep-2014] $)

    $( Two numbers are the canonical representation of a rational iff they are
       coprime and have the right quotient.  (Contributed by Stefan O'Rear,
       13-Sep-2014.) $)
    qnumdenbi $p |- ( ( A e. QQ /\ B e. ZZ /\ C e. NN ) -> ( ( ( B gcd C ) = 1
        /\ A = ( B / C ) ) <-> ( ( numer ` A ) = B /\ ( denom ` A ) = C ) ) )
        $=
      ( va wcel cz cn cfv wceq wa c1st c2nd cgcd co c1 cdiv cop eqeq1d oveq12d
      wb vb w3a cnumer cdenom cxp crio wreu qredeu riotacl 1st2nd2 3syl qnumval
      cq cv qdenval opeq12d eqtr4d 3ad2ant1 fvex opthg 3ad2ant3 opelxpi 3adant1
      bitr2d ax-17 a17d eqeq2d anbi12d riota2f syl2anc op1stg 3ad2ant2 3bitr2rd
      fveq2 op2ndg ) AUMEZBFEZCGEZUBZAUCHZBIAUDHZCIJZDUNZKHZWCLHZMNZOIZAWDWEPNZ
      IZJZDFGUEZUFZBCQZIZWMKHZWMLHZMNZOIZAWOWPPNZIZJZBCMNZOIZABCPNZIZJVSWNVTWAQ
      ZWMIZWBVPVQWNXGTVRVPWLXFWMVPWLWLKHZWLLHZQZXFVPWJDWKUGZWLWKEWLXJIDAUHZWJDW
      KUIWLFGUJUKVPVTXHWAXIDAULDAUOUPUQRURVRVPXGWBTVQVTWABCGAUCUSAUDUSUTVAVDVSW
      MWKEZXKXAWNTVQVRXMVPBCFGVBVCVPVQXKVRXLURWJXADUAWKWMUAUNWMEDVEXMXADVFWCWMI
      ZWGWRWIWTXNWFWQOXNWDWOWEWPMWCWMKVNZWCWMLVNZSRXNWHWSAXNWDWOWEWPPXOXPSVGVHV
      IVJVSWRXCWTXEVSWQXBOVSWOBWPCMVQVPWOBIVRBCFVKVLZVQVRWPCIVPBCFGVOVCZSRVSWSX
      DAVSWOBWPCPXQXRSVGVHVM $.
      $( [13-Sep-2014] $)

    $( The canonical representation of a rational is fully reduced.
       (Contributed by Stefan O'Rear, 13-Sep-2014.) $)
    qnumdencoprm $p |- ( A e. QQ -> ( ( numer ` A ) gcd ( denom ` A ) ) = 1 )
        $=
      ( cq wcel cnumer cdenom cgcd co c1 wceq cdiv wa eqidd eqid jctir cz cn wb
      cfv qnumcl qdencl qnumdenbi mpd3an23 mpbird simpld ) ABCZADRZAERZFGHIZAUF
      UGJGIZUEUHUIKZUFUFIZUGUGIZKZUEUKULUEUFLUGMNUEUFOCUGPCUJUMQASATAUFUGUAUBUC
      UD $.
      $( [13-Sep-2014] $)

    $( Recover a rational number from its canonical representation.
       (Contributed by Stefan O'Rear, 13-Sep-2014.) $)
    qeqnumdivden $p |- ( A e. QQ -> A = ( ( numer ` A ) / ( denom ` A ) ) ) $=
      ( cq wcel cnumer cdenom cgcd co c1 wceq cdiv wa eqidd eqid jctir cz cn wb
      cfv qnumcl qdencl qnumdenbi mpd3an23 mpbird simprd ) ABCZADRZAERZFGHIZAUF
      UGJGIZUEUHUIKZUFUFIZUGUGIZKZUEUKULUEUFLUGMNUEUFOCUGPCUJUMQASATAUFUGUAUBUC
      UD $.
      $( [13-Sep-2014] $)

    $( Multiplying a rational by its denominator results in an integer.
       (Contributed by Stefan O'Rear, 13-Sep-2014.) $)
    qmuldeneqnum $p |- ( A e. QQ -> ( A x. ( denom ` A ) ) = ( numer ` A ) ) $=
      ( cq wcel cdenom cfv cmul co cnumer cdiv qeqnumdivden oveq1d cc0 wne wceq
      cc cz qnumcl zcn syl cn qdencl nncn nnne0 divcan1 syl3anc eqtrd ) ABCZAAD
      EZFGAHEZUHIGZUHFGZUIUGAUJUHFAJKUGUIOCZUHOCZUHLMZUKUINUGUIPCULAQUIRSUGUHTC
      ZUMAUAZUHUBSUGUOUNUPUHUCSUIUHUDUEUF $.
      $( [13-Sep-2014] $)

    $( A number is an integer iff its negative is.  (Contributed by Stefan
       O'Rear, 13-Sep-2014.) $)
    znegclb $p |- ( A e. CC -> ( A e. ZZ <-> -u A e. ZZ ) ) $=
      ( cc wcel cz cneg znegcl negneg eleq1d syl5ib impbid2 ) ABCZADCZAEZDCZAFN
      MEZDCKLMFKOADAGHIJ $.
      $( [13-Sep-2014] $)

    $( A number which is less than zero is not zero.  (Contributed by Stefan
       O'Rear, 13-Sep-2014.) $)
    lt0ne0 $p |- ( ( A e. RR /\ A < 0 ) -> A =/= 0 ) $=
      ( cr wcel cc0 clt wbr wa wne 0re ltne mp3an2 necomd ) ABCZADEFZGDAMDBCNDA
      HIADJKL $.
      $( [13-Sep-2014] $)

    $( Strong form of ~ divides2 for natural numbers.  (Contributed by Stefan
       O'Rear, 13-Sep-2014.) $)
    nndivdivides $p |- ( ( A e. NN /\ B e. NN ) -> ( B || A <-> ( A / B ) e. NN
        ) ) $=
      ( cn wcel wa cdivides wbr cc0 cdiv co clt cz wne wb adantl adantr cr nnre
      nnz nngt0 nnne0 divides2 syl3anc anbi1d divgt0 syl22anc elnnz a1i 3bitr4d
      biantrud ) ACDZBCDZEZBAFGZHABIJZKGZEUOLDZUPEZUNUOCDZUMUNUQUPUMBLDZBHMZALD
      ZUNUQNULUTUKBSOULVAUKBUAOUKVBULASPBAUBUCUDUMUPUNUMAQDZHAKGZBQDZHBKGZUPUKV
      CULARPUKVDULATPULVEUKBROULVFUKBTOABUEUFUJUSURNUMUOUGUHUI $.
      $( [13-Sep-2014] $)

    $( Calculate the reduced form of a quotient using ` gcd ` .  (Contributed
       by Stefan O'Rear, 13-Sep-2014.) $)
    divnumden $p |- ( ( A e. ZZ /\ B e. NN ) -> ( ( numer ` ( A / B ) ) = ( A /
        ( A gcd B ) ) /\ ( denom ` ( A / B ) ) = ( B / ( A gcd B ) ) ) ) $=
      ( cz wcel cn wa cgcd co cdiv c1 wceq cfv cdivides wbr adantl cc0 wn cc wb
      syl cnumer cdenom simpl nnz wne nnne0 df-ne sylib intnand gcdn0cl gcddvds
      syl21anc sylan2 gcddiv syl31anc nncn divid syl2anc eqtr3d zcn w3a divcan7
      adantr eqcomd syl122anc znq simpld cn0 gcdcl nn0z divides2 syl3anc simprd
      cq mpbid simpr nndivdivides qnumdenbi mpbi2and ) ACDZBEDZFZAABGHZIHZBWCIH
      ZGHZJKZABIHZWDWEIHZKZWHUALWDKWHUBLWEKFZWBWCWCIHZWFJWBVTBCDZWCEDZWCAMNZWCB
      MNZFZWLWFKVTWAUCZWAWMVTBUDZOZWBVTWMAPKZBPKZFQWNWRWTWBXBXAWAXBQZVTWABPUEZX
      CBUFZBPUGUHOUIABUJULZWAVTWMWQWSABUKUMZABWCUNUOWBWCRDZWCPUEZWLJKWBWNXHXFWC
      UPTZWBWNXIXFWCUFTZWCUQURUSWBARDZBRDZXDXHXIWJVTXLWAAUTVCWAXMVTBUPOWAXDVTXE
      OXJXKXLXMXDFXHXIFVAWIWHABWCVBVDVEWBWHVNDWDCDZWEEDZWGWJFWKSABVFWBWOXNWBWOW
      PXGVGWBWCCDZXIVTWOXNSWAVTWMXPWSVTWMFWCVHDXPABVIWCVJTUMXKWRWCAVKVLVOWBWPXO
      WBWOWPXGVMWBWAWNWPXOSVTWAVPXFBWCVQURVOWHWDWEVRVLVS $.
      $( [13-Sep-2014] $)

    $( Reducing a quotient never increases the denominator.  (Contributed by
       Stefan O'Rear, 13-Sep-2014.) $)
    divdenle $p |- ( ( A e. ZZ /\ B e. NN ) -> ( denom ` ( A / B ) ) <_ B ) $=
      ( cz wcel cn wa cdiv co cfv cle wceq c1 wbr cc0 wn adantl syl cr clt a1i
      cdenom cgcd cnumer divnumden simprd simpl nnz nnne0 df-ne intnand gcdn0cl
      wne sylib syl21anc nnge1 wb 1re lt01 nnre nngt0 lediv2 syl222anc mpbid cc
      nncn div1 breqtrd eqbrtrd ) ACDZBEDZFZABGHZUAIZBABUBHZGHZBJVKVLUCIAVNGHKV
      MVOKABUDUEVKVOBLGHZBJVKLVNJMZVOVPJMZVKVNEDZVQVKVIBCDZANKZBNKZFOVSVIVJUFVJ
      VTVIBUGPVKWBWAVJWBOZVIVJBNULWCBUHBNUIUMPUJABUKUNZVNUOQVKLRDZNLSMZVNRDZNVN
      SMZBRDZNBSMZVQVRUPWEVKUQTWFVKURTVKVSWGWDVNUSQVKVSWHWDVNUTQVJWIVIBUSPVJWJV
      IBUTPLVNBVAVBVCVKBVDDZVPBKVJWKVIBVEPBVFQVGVH $.
      $( [13-Sep-2014] $)

    $( A rational is positive iff its canonical numerator is.  (Contributed by
       Stefan O'Rear, 15-Sep-2014.) $)
    qnumgt0 $p |- ( A e. QQ -> ( 0 < A <-> 0 < ( numer ` A ) ) ) $=
      ( cq wcel cc0 clt wbr cdenom cfv cmul co cnumer cr wb 0re a1i qre cn nnre
      qdencl syl nngt0 ltmul1 syl112anc cc wceq nncn mul02 qmuldeneqnum breq12d
      3syl bitrd ) ABCZDAEFZDAGHZIJZAUNIJZEFZDAKHZEFULDLCZALCUNLCZDUNEFZUMUQMUS
      ULNOAPULUNQCZUTASZUNRTULVBVAVCUNUATDAUNUBUCULUODUPUREULVBUNUDCUODUEVCUNUF
      UNUGUJAUHUIUK $.
      $( [15-Sep-2014] $)

    $( A rational is positive iff its canonical numerator is a natural number.
       (Contributed by Stefan O'Rear, 15-Sep-2014.) $)
    qgt0numnn $p |- ( ( A e. QQ /\ 0 < A ) -> ( numer ` A ) e. NN ) $=
      ( cq wcel cc0 clt wbr wa cnumer cfv cz qnumcl adantr qnumgt0 biimpa elnnz
      cn sylanbrc ) ABCZDAEFZGAHIZJCZDTEFZTPCRUASAKLRSUBAMNTOQ $.
      $( [15-Sep-2014] $)

    $( The square of a rational is rational.  (Contributed by Stefan O'Rear,
       15-Sep-2014.) $)
    qsqcl $p |- ( A e. QQ -> ( A ^ 2 ) e. QQ ) $=
      ( cq wcel c2 cexp co cmul cc wceq qcn sqval syl qmulcl anidms eqeltrd ) A
      BCZADEFZAAGFZBPAHCQRIAJAKLPRBCAAMNO $.
      $( [15-Sep-2014] $)

    $( Squaring commutes with GCD, in particular two coprime numbers have
       coprime squares.  (Contributed by Stefan O'Rear, 15-Sep-2014.) $)
    nn0gcdsq $p |- ( ( A e. NN0 /\ B e. NN0 ) -> ( ( A gcd B ) ^ 2 ) = ( ( A ^
        2 ) gcd ( B ^ 2 ) ) ) $=
      ( cn0 wcel cn cc0 wceq wo cgcd co c2 cexp wa cabs cfv syl cz oveq1d oveq1
      sq0 elnn0 sqgcd nncn abssq nnz gcd0id a1i zsqcl 3syl eqtrd 3eqtr4d adantl
      cc eqeq12d adantr mpbird gcdid0 oveq2d oveq2 gcd0val oveq1i oveq12i eqtri
      wb 3eqtr4i oveq12 oveqan12d 3eqtr4a ccase syl2anb ) ACDAEDZAFGZHBEDZBFGZH
      ABIJZKLJZAKLJZBKLJZIJZGZBCDAUABUAVKVMVLVNVTABUBVLVMMVTFBIJZKLJZFKLJZVRIJZ
      GZVMWEVLVMBNOZKLJZVRNOZWBWDVMBUMDWGWHGBUCBUDPVMWAWFKLVMBQDZWAWFGBUEZBUFPR
      VMWDFVRIJZWHVMWCFVRIWCFGZVMTUGRVMWIVRQDWKWHGWJBUHVRUFUIUJUKULVLVTWEVDVMVL
      VPWBVSWDVLVOWAKLAFBISRVLVQWCVRIAFKLSZRUNUOUPVKVNMVTAFIJZKLJZVQWCIJZGZVKWQ
      VNVKANOZKLJZVQNOZWOWPVKAUMDWSWTGAUCAUDPVKWNWRKLVKAQDZWNWRGAUEZAUQPRVKWPVQ
      FIJZWTVKWCFVQIWLVKTUGURVKXAVQQDXCWTGXBAUHVQUQUIUJUKUOVNVTWQVDVKVNVPWOVSWP
      VNVOWNKLBFAIUSRVNVRWCVQIBFKLSZURUNULUPVLVNMZFFIJZKLJZWCWCIJZVPVSWCFXGXHTX
      FFKLUTVAXHXFFWCFWCFITTVBUTVCVEXEVOXFKLAFBFIVFRVLVNVQWCVRWCIWMXDVGVHVIVJ
      $.
      $( [15-Sep-2014] $)

    $( ~ nn0gcdsq extended to integers by symmetry.  (Contributed by Stefan
       O'Rear, 15-Sep-2014.) $)
    zgcdsq $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( ( A gcd B ) ^ 2 ) = ( ( A ^ 2 )
        gcd ( B ^ 2 ) ) ) $=
      ( cz wcel wa cgcd co c2 cexp cabs cfv gcdabs eqcomd cn0 wceq nn0abscl zre
      cr absresq syl oveq1d nn0gcdsq syl2an adantr adantl oveq12d 3eqtrd ) ACDZ
      BCDZEZABFGZHIGAJKZBJKZFGZHIGZULHIGZUMHIGZFGZAHIGZBHIGZFGUJUKUNHIUJUNUKABL
      MUAUHULNDUMNDUOUROUIAPBPULUMUBUCUJUPUSUQUTFUJARDZUPUSOUHVAUIAQUDASTUJBRDZ
      UQUTOUIVBUHBQUEBSTUFUG $.
      $( [15-Sep-2014] $)

    $( Squaring a rational squares its canonical components.  (Contributed by
       Stefan O'Rear, 15-Sep-2014.) $)
    numdensq $p |- ( A e. QQ -> ( ( numer ` ( A ^ 2 ) ) = ( ( numer ` A ) ^ 2 )
        /\ ( denom ` ( A ^ 2 ) ) = ( ( denom ` A ) ^ 2 ) ) ) $=
      ( cq wcel cnumer cfv c2 cexp co cdenom cgcd c1 wceq cdiv wa oveq1d cz syl
      cn cc syl3anc qnumdencoprm qnumcl qdencl nnz syl2anc sq1 a1i qeqnumdivden
      zgcdsq 3eqtr3d cc0 wne zcn nnne0 sqdiv eqtrd qsqcl zsqcl nnsqcl qnumdenbi
      nncn wb mpbi2and ) ABCZADEZFGHZAIEZFGHZJHZKLZAFGHZVFVHMHZLZVKDEVFLVKIEVHL
      NZVDVEVGJHZFGHZKFGHZVIKVDVOKFGAUAOVDVEPCZVGPCZVPVILAUBZVDVGRCZVSAUCZVGUDQ
      VEVGUIUEVQKLVDUFUGUJVDVKVEVGMHZFGHZVLVDAWCFGAUHOVDVESCZVGSCZVGUKULZWDVLLV
      DVRWEVTVEUMQVDWAWFWBVGVAQVDWAWGWBVGUNQVEVGUOTUPVDVKBCVFPCZVHRCZVJVMNVNVBA
      UQVDVRWHVTVEURQVDWAWIWBVGUSQVKVFVHUTTVC $.
      $( [15-Sep-2014] $)

    $( Square commutes with canonical numerator.  (Contributed by Stefan
       O'Rear, 15-Sep-2014.) $)
    numsq $p |- ( A e. QQ -> ( numer ` ( A ^ 2 ) ) = ( ( numer ` A ) ^ 2 ) ) $=
      ( cq wcel c2 cexp co cnumer cfv wceq cdenom numdensq simpld ) ABCADEFZGHA
      GHDEFIMJHAJHDEFIAKL $.
      $( [15-Sep-2014] $)

    $( Square commutes with canonical denominator.  (Contributed by Stefan
       O'Rear, 15-Sep-2014.) $)
    densq $p |- ( A e. QQ -> ( denom ` ( A ^ 2 ) ) = ( ( denom ` A ) ^ 2 ) ) $=
      ( cq wcel c2 cexp co cnumer cfv wceq cdenom numdensq simprd ) ABCADEFZGHA
      GHDEFIMJHAJHDEFIAKL $.
      $( [15-Sep-2014] $)

    $( A rational is an integer iff it has denominator 1.  (Contributed by
       Stefan O'Rear, 15-Sep-2014.) $)
    qden1elz $p |- ( A e. QQ -> ( ( denom ` A ) = 1 <-> A e. ZZ ) ) $=
      ( cq wcel cdenom cfv c1 wceq cz wa cnumer cdiv co adantr cc zcn div1 3syl
      cle wbr cn qeqnumdivden oveq2 adantl qnumcl 3eqtrd eqeltrd simpr divdenle
      fveq2d 1nn sylancl eqbrtrrd wb qdencl nnle1eq1 syl mpbid impbida ) ABCZAD
      EZFGZAHCZUSVAIZAAJEZHVCAVDUTKLZVDFKLZVDUSAVEGVAAUAMVAVEVFGUSUTFVDKUBUCVCV
      DHCZVDNCVFVDGUSVGVAAUDMZVDOVDPQUEVHUFUSVBIZUTFRSZVAVIAFKLZDEZUTFRVIVKADVI
      VBANCVKAGUSVBUGZAOAPQUIVIVBFTCVLFRSVMUJAFUHUKULVIUTTCZVJVAUMUSVNVBAUNMUTU
      OUPUQUR $.
      $( [15-Sep-2014] $)

    $( If an integer has a rational square root, that root is must be an
       integer.  (Contributed by Stefan O'Rear, 15-Sep-2014.) $)
    zsqrelqelz $p |- ( ( A e. ZZ /\ ( sqr ` A ) e. QQ ) -> ( sqr ` A ) e. ZZ )
        $=
      ( cz wcel cfv cq cdenom c1 wceq c2 cexp co a1i syl adantr qden1elz adantl
      wb cr cc0 cle csqr wa sq1 cc zcn sqrth fveq2d simpl zq mpbird eqtrd densq
      3eqtr2rd wbr qdencl nnre cn0 nnnn0 nn0ge0 3syl 1nn0 nn0ge0i sq11 syl22anc
      cn 1re mpbid ) ABCZAUADZECZUBZVIFDZGHZVIBCZVKVLIJKZGIJKZHZVMVKVPGVIIJKZFD
      ZVOVPGHVKUCLVKVSAFDZGVKVRAFVHVRAHZVJVHAUDCWAAUEAUFMNUGVKVTGHZVHVHVJUHVKAE
      CZWBVHQVHWCVJAUINAOMUJUKVJVSVOHVHVIULPUMVKVLRCZSVLTUNZGRCZSGTUNZVQVMQVKVL
      VECZWDVJWHVHVIUOPZVLUPMVKWHVLUQCWEWIVLURVLUSUTWFVKVFLWGVKGVAVBLVLGVCVDVGV
      JVMVNQVHVIOPVG $.
      $( [15-Sep-2014] $)

    $( Any integer strictly between two adjacent squares has an irrational
       square root.  (Contributed by Stefan O'Rear, 15-Sep-2014.) $)
    nonsq $p |- ( ( ( A e. NN0 /\ B e. NN0 ) /\ ( ( B ^ 2 ) < A /\ A < ( ( B +
        1 ) ^ 2 ) ) ) -> -. ( sqr ` A ) e. QQ ) $=
      ( cn0 wcel wa c2 cexp co clt wbr cz nn0z ad2antlr cr ad2antrr syl cc0 cle
      nn0re nn0ge0 c1 caddc csqr cq wn simprl wceq recnd sqrth breqtrrd resqrcl
      cfv cc wb syl2anc sqrge0 lt2sq syl22anc mpbird eqbrtrd peano2re peano2nn0
      simprr btwnnz syl3anc wi zsqrelqelz ex mtod ) ACDZBCDZEZBFGHZAIJZABUAUBHZ
      FGHZIJZEZEZAUCULZUDDZVTKDZVSBKDZBVTIJZVTVOIJZWBUEVKWCVJVRBLMVSWDVMVTFGHZI
      JZVSVMAWFIVLVNVQUFVSAUMDWFAUGVSAVJANDZVKVRASOZUHAUIPZUJVSBNDZQBRJZVTNDZQV
      TRJZWDWGUNVKWKVJVRBSMZVKWLVJVRBTMVSWHQARJZWMWIVJWPVKVRATOZAUKUOZVSWHWPWNW
      IWQAUPUOZBVTUQURUSVSWEWFVPIJZVSWFAVPIWJVLVNVQVCUTVSWMWNVONDZQVORJZWEWTUNW
      RWSVSWKXAWOBVAPVKXBVJVRVKVOCDXBBVBVOTPMVTVOUQURUSBVTVDVEVSAKDZWAWBVFVJXCV
      KVRALOXCWAWBAVGVHPVI $.
      $( [15-Sep-2014] $)
  $}

