$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    The Ackermann bijection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A set is equinumerous to its cross-product with a singleton on the left.
     (Contributed by Stefan O'Rear, 21-Nov-2014.) $)
  xpsnen2g $p |- ( ( A e. V /\ B e. W ) -> ( { A } X. B ) ~~ B ) $=
    ( wcel wa csn cxp cen wbr cvv snex xpcomeng mpan adantl xpsneng ancoms entr
    syl2anc ) ACEZBDEZFAGZBHZBUBHZIJZUDBIJZUCBIJUAUETUBKEUAUEALUBBKDMNOUATUFBAD
    CPQUCUDBRS $.
    $( [21-Nov-2014] $)

  ${
    $d x A $.

    $( Disjoint union is a subset of a cross product.  (Contributed by Stefan
       O'Rear, 21-Nov-2014.) $)
    djussxp $p |- U_ x e. A ( { x } X. B ) C_ ( A X. _V ) $=
      ( cv csn cxp ciun cvv wss iunss wcel snssi ssv xpss12 sylancl mprgbir ) A
      BADZEZCFZGBHFZISTIZABABSTJQBKRBICHIUAQBLCMRBCHNOP $.
      $( [21-Nov-2014] $)

    $d y B $.

    $( Disjoint unions with disjoint index sets are disjoint.  (Contributed by
       Stefan O'Rear, 21-Nov-2014.) $)
    djudisj $p |- ( ( A i^i B ) = (/) -> ( U_ x e. A ( { x } X. C ) i^i
            U_ y e. B ( { y } X. D ) ) = (/) ) $=
      ( cin c0 wceq cv csn cxp ciun cvv wss djussxp incom syl5eq ssdisj sylancr
      xpdisj1 ) CDGHIZACAJKELMZCNLZOUDBDBJKFLMZGZHIUCUEGHIACEPUBUFUEUDGZHUDUEQU
      BUEDNLZOUHUDGZHIUGHIBDFPUBUIUDUHGHUHUDQCDNNUARUEUHUDSTRUCUDUEST $.
      $( [21-Nov-2014] $)
  $}

  ${
    $d F a b c x y $.  $d G a b c x y $.  $d H a b c x y $.  $d A a b c x y $.
    $d B a b c x y $.

    $( A one-to-one mapping induces a one-to-one mapping on power sets.
       (Contributed by Stefan O'Rear, 18-Nov-2014.) $)
    f1opw $p |- ( F : A -1-1-onto-> B -> ( b e. ~P A |-> ( F " b ) ) :
          ~P A -1-1-onto-> ~P B ) $=
      ( va cpw cv cima wcel wss crn imassrn wceq syl5sseq wfun cvv vex funimaex
      wb adantr wf1o ccnv cmpt eqid wfo f1ofo forn syl f1ofun elpwg 3syl mpbird
      dfdm4 f1odm syl5eqr dff1o3 simprbi wa elpwi adantl foimacnv syl2an eqcomd
      cdm imaeq2 eqeq2d syl5ibrcom wf1 f1of1 f1imacnv impbid f1o2d ) ABCUAZDEAF
      ZBFZCDGZHZCUBZEGZHZDVNVQUCZWAUDVMVQVOIZVPVNIZVMWBVQBJZVMCKZVQBCVPLVMABCUE
      ZWEBMABCUFZABCUGUHNVMCOVQPIWBWDSABCUICVPDQRVQBPUJUKULTVMVTVNIZVSVOIZVMWHV
      TAJZVMVRKZVTAVRVSLVMWKCVDACUMABCUNUONVMVROZVTPIWHWJSVMWFWLABCUPUQVRVSEQRV
      TAPUJUKULTVMWCWIURZURZVPVTMZVSVQMZWNWPWOVSCVTHZMWNWQVSVMWFVSBJZWQVSMWMWGW
      IWRWCVSBUSUTABVSCVAVBVCWOVQWQVSVPVTCVEVFVGWNWOWPVPVRVQHZMWNWSVPVMABCVHVPA
      JZWSVPMWMABCVIWCWTWIVPAUSTABVPCVJVBVCWPVTWSVPVSVQVRVEVFVGVKVL $.
      $( [18-Nov-2014] $)

    $( An ordinal number is a proper subset of its successor.  (Contributed by
       Stefan O'Rear, 18-Nov-2014.) $)
    onpsssuc $p |- ( A e. On -> A C. suc A ) $=
      ( con0 wcel csuc wpss sucidg wb eloni ordsuc sylib ordelpss syl2anc mpbid
      word ) ABCZAADZCZAPEZABFOANZPNZQRGAHZOSTUAAIJAPKLM $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij2lem1 $p |- ( A e. om -> ~P A C_ ( ~P om i^i Fin ) ) $=
      ( va com wcel cpw cfn cin cv wa wss word ordom ordelss sspwb sylib sselda
      mpan con0 onfin2 inss2 eqsstri sseli elpwi ssfi syl2an sylanbrc ex ssrdv
      elin ) ACDZBAEZCEZFGZUJBHZUKDZUNUMDZUJUOIUNULDUNFDZUPUJUKULUNUJACJZUKULJC
      KUJURLCAMQACNOPUJAFDUNAJUQUOCFACRFGFSRFTUAUBUNAUCAUNUDUEUNULFUIUFUGUH $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem1 $p |- ( -. A e. B -> ( B i^i suc A ) = ( B i^i A ) ) $=
      ( wcel wn csuc cin csn cun df-suc ineq2i indi eqtri c0 wceq disjsn uneq2d
      biimpri un0 syl6eq syl5eq ) ABCDZBAEZFZBAFZBAGZFZHZUDUCBAUEHZFUGUBUHBAIJB
      AUEKLUAUGUDMHUDUAUFMUDUFMNUABAOQPUDRST $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem2 $p |- ( A e. B -> ( B i^i suc A ) =
        ( { A } u. ( B i^i A ) ) ) $=
      ( wcel csuc cin csn cun df-suc ineq2i indi uncom 3eqtri wss snssi sseqin2
      wceq sylib uneq1d syl5eq ) ABCZBADZEZBAFZEZBAEZGZUCUEGUBBAUCGZEUEUDGUFUAU
      GBAHIBAUCJUEUDKLTUDUCUETUCBMUDUCPABNUCBOQRS $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem3 $p |- ( A e. om -> A e. ( ~P om i^i Fin ) ) $=
      ( com wcel cpw cfn cin wss word ordom mpan elpwg mpbird con0 onfin2 inss2
      ordelss eqsstri sseli elin sylanbrc ) ABCZABDZCZAECAUBEFCUAUCABGZBHUAUDIB
      APJABBKLBEABMEFENMEOQRAUBEST $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem4 $p |- ( A e. om -> { A } e. ( ~P om i^i Fin ) ) $=
      ( com wcel csn cpw cfn cin snelpwi snfi a1i elin sylanbrc ) ABCZADZBEZCNF
      CZNOFGCABHPMAIJNOFKL $.
      $( [19-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem5 $p |- ( A e. om -> ( card ` ~P suc A ) =
        ( ( card ` ~P A ) +o ( card ` ~P A ) ) ) $=
      ( va cpw ccrd cfv coa co wceq com wcel ccda cen wbr ovex c2o cxp cmap cfn
      entr syl csuc suceq pweqd fveq2d pweq oveq12d eqeq12d cvv vex sucex pw2en
      cv csn cun df-suc oveq2i word cin c0 nnord orddisj snex 2onn mapunen 3syl
      elexi enref mapsnen xpen sylancl syl5eqbr xpex ensymi sylancr pwex xp2cda
      mp2an syl6breq con0 onfin2 inss2 eqsstri sseli pwfi ficardid fvex syl2anc
      sylib cdaen ensym carden2b mpsyl ficardom nnacda eqtrd vtoclga ) BULZUAZC
      ZDEZWQCZDEZXBFGZHAUAZCZDEZACZDEZXHFGZHBAIWQAHZWTXFXCXIXJWSXEDXJWRXDWQAUBU
      CUDXJXBXHXBXHFXJXAXGDWQAUEUDZXKUFUGWQIJZWTXBXBKGZDEZXCXMUHJXLWSXMLMZWTXNH
      XBXBKNXLWSXAXAKGZLMXPXMLMZXOXLWSXAOPZXPLXLWSOWRQGZLMXSXRLMZWSXRLMWRWQBUIZ
      UJUKXLXSOWQQGZOPZLMYCXRLMXTXLXSOWQWQUMZUNZQGZYCLWRYEOQWQUOUPXLYFYBOYDQGZP
      ZLMZYHYCLMZYFYCLMXLWQUQWQYDURUSHYIWQUTWQVAWQYDOYAWQVBOIVCVFZVDVEYBYBLMYGO
      LMYJYBOWQQNZVGOWQYAVHYBYBYGOYLYKVIVQYFYHYCSVJVKXRYCYBOYLYKVLXAYBLMOOLMXRY
      CLMWQYAUKOYKVGXAYBOOYLYKVIVQVMXSYCXRSVJWSXSXRSVNXAWQYAVOZVPVRXLXMXPLMZXQX
      LXBXALMZYOYNXLXARJZYOXLWQRJYPIRWQIVSRURRVTVSRWAWBWCWQWDWHZXAWETZYRXBXAXBX
      AXADWFZYMYSYMWIWGXMXPXAXAKNWJTWSXPXMSWGWSXMUHWKWLXLXBIJZYTXNXCHXLYPYTYQXA
      WMTZUUAXBXBWNWGWOWP $.
      $( [19-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij1lem6 $p |- ( ( A e. ( ~P om i^i Fin ) /\
          B e. ( ~P om i^i Fin ) ) ->
        ( A u. B ) e. ( ~P om i^i Fin ) ) $=
      ( com cpw cfn cin wcel cun wss inss1 sseli elpwi simpl simpr unssd syl2an
      wa wb inss2 unfi elpwg syl mpbird elin sylanbrc ) ACDZEFZGZBUGGZQZABHZUFG
      ZUKEGZUKUGGUJULUKCIZUHAUFGZBUFGZUNUIUGUFAUFEJZKUGUFBUQKUOACIZBCIZUNUPACLB
      CLURUSQABCURUSMURUSNOPPUJUMULUNRUHAEGBEGUMUIUGEAUFESZKUGEBUTKABTPZUKCEUAU
      BUCVAUKUFEUDUE $.
      $( [18-Nov-2014] $)

    ackbij.f $e |- F = ( x e. ( ~P om i^i Fin ) |->
        ( card ` U_ y e. x ( { y } X. ~P y ) ) ) $.
    $( Lemma for ~ ackbij1 . $)
    ackbij1lem7 $p |- ( A e. ( ~P om i^i Fin ) -> ( F ` A ) =
        ( card ` U_ y e. A ( { y } X. ~P y ) ) ) $=
      ( cv csn cpw cxp ciun ccrd cfv com cfn cin wceq iuneq1 fveq2d fvex fvmpt
      ) ACBAFZBFZGUBHIZJZKLBCUCJZKLMHNODUACPUDUEKBUACUCQREUEKST $.
      $( [21-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem8 $p |- ( A e. om -> ( F ` { A } ) = ( card ` ~P A ) ) $=
      ( va cv csn cfv cpw ccrd wceq com sneq fveq2d pweq eqeq12d wcel cxp cvv
      cfn cin ackbij1lem4 ackbij1lem7 syl vex weq xpeq12d iunxsn fveq2i cen wbr
      ciun pwex xpsnen2g mp2an carden2b mp2 eqtri syl6eq vtoclga ) FGZHZDIZVBJZ
      KIZLCHZDIZCJZKIZLFCMVBCLZVDVHVFVJVKVCVGDVBCNOVKVEVIKVBCPOQVBMRZVDBVCBGZHZ
      VMJZSZUMZKIZVFVLVCMJUAUBRVDVRLVBUCABVCDEUDUEVRVCVESZKIZVFVQVSKBVBVPVSFUFZ
      BFUGVNVCVOVEVMVBNVMVBPUHUIUJVETRZVSVEUKULZVTVFLVBWAUNZVBTRWBWCWAWDVBVETTU
      OUPVSVETUQURUSUTVA $.
      $( [19-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem9 $p |- ( ( A e. ( ~P om i^i Fin ) /\ B e. ( ~P om i^i Fin ) /\
          ( A i^i B ) = (/) ) ->
        ( F ` ( A u. B ) ) = ( ( F ` A ) +o ( F ` B ) ) ) $=
      ( com cfn cin wcel wceq ciun ccrd cfv co cen wbr sseli syl syl2anc cpw c0
      w3a cun cv csn cxp coa ccda iunxun wral inss2 3ad2ant1 wa wss inss1 elpwi
      snfi con0 onfin2 eqsstri syl6ss sselda pwfi sylib sylancr ralrimiva iunfi
      xpfi 3ad2ant2 syl5eqel ficardom ficardid cdaeng syl222anc 3ad2ant3 cdaung
      unfi djudisj syl3anc syl6breqr carden2b nnacda eqtr3d ackbij1lem6 3adant3
      entr sylc ackbij1lem7 oveqan12d 3eqtr4d ) CGUAZHIZJZDWMJZCDIUBKZUCZBCDUDZ
      BUEZUFZWSUAZUGZLZMNZBCXBLZMNZBDXBLZMNZUHOZWRENZCENZDENZUHOZWQXFXHUIOZMNZX
      DXIWQXCHJXNXCPQZXOXDKWQXCXEXGUDZHBCDXBUJZWQXEHJZXGHJZXQHJWQCHJZXBHJZBCUKX
      SWNWOYAWPWMHCWLHULZRUMWQYBBCWQWSCJUNZWTHJZXAHJZYBWSURZYDWSHJZYFWQCHWSWQCG
      HWNWOCGUOZWPWNCWLJYIWMWLCWLHUPZRCGUQSUMGUSHIHUTUSHULVAZVBVCWSVDZVEWTXAVIZ
      VFVGBCXBVHTZWQDHJZYBBDUKXTWOWNYOWPWMHDYCRVJWQYBBDWQWSDJUNZYEYFYBYGYPYHYFW
      QDHWSWQDGHWOWNDGUOZWPWODWLJYQWMWLDYJRDGUQSVJYKVBVCYLVEYMVFVGBDXBVHTZXEXGV
      RTVKWQXNXEXGUIOZPQZYSXCPQXPWQXFGJZXSXHGJZXTXFXEPQZXHXGPQZYTWQXSUUAYNXEVLS
      ZYNWQXTUUBYRXGVLSZYRWQXSUUCYNXEVMSWQXTUUDYRXGVMSXFXEXHXGGHGHVNVOWQYSXQXCP
      WQXSXTXEXGIUBKZYSXQPQYNYRWPWNUUGWOBBCDXAXAVSVPXEXGHHVQVTXRWAXNYSXCWGTXNXC
      HWBWHWQUUAUUBXOXIKUUEUUFXFXHWCTWDWQWRWMJZXJXDKWNWOUUHWPCDWEWFABWREFWISWNW
      OXMXIKWPWNWOXKXFXLXHUHABCEFWIABDEFWIWJWFWK $.
      $( [19-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem10 $p |- F : ( ~P om i^i Fin ) --> om $=
      ( com cpw cfn cin wf wtru cv csn cxp ciun ccrd wcel inss2 sseli syl con0
      cfv wral wel snfi wss inss1 elpwi onfin2 eqsstri syl6ss sselda pwfi sylib
      wa xpfi sylancr ralrimiva iunfi syl2anc ficardom adantl fmptd trud ) EFZG
      HZECIJAVEBAKZBKZLZVGFZMZNZOUAZECVFVEPZVLEPZJVMVKGPZVNVMVFGPVJGPZBVFUBVOVE
      GVFVDGQRVMVPBVFVMBAUCUNZVHGPVIGPZVPVGUDVQVGGPVRVMVFGVGVMVFEGVMVFVDPVFEUEV
      EVDVFVDGUFRVFEUGSETGHGUHTGQUIUJUKVGULUMVHVIUOUPUQBVFVJURUSVKUTSVADVBVC $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem11 $p |- ( ( A e. ( ~P om i^i Fin ) /\ B C_ A ) ->
        B e. ( ~P om i^i Fin ) ) $=
      ( com cpw cfn cin wcel wss wa inss1 sseli elpwi syl sstr sylan2 cvv ssexg
      wb elpwg mpbird ancoms inss2 ssfi sylan elin sylanbrc ) CGHZIJZKZDCLZMDUK
      KZDIKZDULKUNUMUOUNUMMZUODGLZUMUNCGLZURUMCUKKUSULUKCUKINOCGPQDCGRSUQDTKUOU
      RUBDCULUADGTUCQUDUEUMCIKUNUPULICUKIUFOCDUGUHDUKIUIUJ $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem12 $p |- ( ( B e. ( ~P om i^i Fin ) /\ A C_ B ) ->
        ( F ` A ) C_ ( F ` B ) ) $=
      ( com cpw cfn cin wcel wss cfv cdif ackbij1lem11 ffvelrn sylancr a1i wceq
      wa coa co wf ackbij1lem10 syldan nnaword1 syl2anc cun disjdif ackbij1lem9
      difss c0 syl3anc undif biimpi adantl fveq2d eqtr3d sseqtrd ) DGHIJZKZCDLZ
      TZCEMZVDDCNZEMZUAUBZDEMZVCVDGKZVFGKZVDVGLVCUTGEUCZCUTKZVIABEFUDZABDCEFOZU
      TGCEPQVCVKVEUTKZVJVMVAVBVEDLZVOVPVCDCUKRABDVEEFOUEZUTGVEEPQVDVFUFUGVCCVEU
      HZEMZVGVHVCVLVOCVEJULSZVSVGSVNVQVTVCCDUIRABCVEEFUJUMVCVRDEVBVRDSZVAVBWACD
      UNUOUPUQURUS $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem13 $p |- ( F ` (/) ) = (/) $=
      ( c0 cfv coa co wceq cun com wcel cpw cfn ackbij1lem10 peano1 f0cli ax-mp
      cin mp3an nna0 un0 fveq2i ackbij1lem3 ackbij1lem9 3eqtr2ri wb nnacan mpbi
      in0 ) ECFZUKGHZUKEGHZIZUKEIZUMUKEEJZCFZULUKKLZUMUKIKMNSZKECABCDOPQZUKUARU
      PECEUBUCEUSLZVAEESEIUQULIEKLZVAPEUDRZVCEUJABEECDUETUFURURVBUNUOUGUTUTPUKU
      KEUHTUI $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem14 $p |- ( A e. om -> ( F ` { A } ) = suc ( F ` A ) ) $=
      ( com wcel cfv cpw ccrd csuc wceq c0 pweq fveq2d suceq syl coa adantr cfn
      va vb csn ackbij1lem8 cv fveq2 eqeq12d weq c1o df-1o pw0 fveq2i cvv ax-mp
      0ex cardsn eqtri ackbij1lem13 3eqtr4i wa co adantl ackbij1lem5 cun df-suc
      oveq2 equncomi cin ackbij1lem4 ackbij1lem3 incom word orddisj ackbij1lem9
      nnord syl5eq syl3anc oveq1d eqtrd con0 onfin2 inss2 eqsstri pwfi ficardom
      sseli sylib ackbij1lem10 ffvelrni nnasuc syl2anc eqtr4d 3eqtr4d ex finds
      ) CFGCUCDHCIZJHZCDHZKZABCDEUDUAUEZIZJHZWTDHZKZLMIZJHZMDHZKZLUBUEZIZJHZXID
      HZKZLZXIKZIZJHZXODHZKZLZWQWSLUAUBCWTMLZXBXFXDXHYAXAXEJWTMNOYAXCXGLXDXHLWT
      MDUFXCXGPQUGUAUBUHZXBXKXDXMYBXAXJJWTXINOYBXCXLLXDXMLWTXIDUFXCXLPQUGWTXOLZ
      XBXQXDXSYCXAXPJWTXONOYCXCXRLXDXSLWTXODUFXCXRPQUGWTCLZXBWQXDWSYDXAWPJWTCNO
      YDXCWRLXDWSLWTCDUFXCWRPQUGUIMKZXFXHUJXFMUCZJHZUIXEYFJUKULMUMGYGUILUOMUMUP
      UNUQXGMLXHYELABDEURXGMPUNUSXIFGZXNXTYHXNUTZXKXKRVAZXKXMRVAZXQXSXNYJYKLYHX
      KXMXKRVFVBYHXQYJLXNXIVCSYIXSXKXLRVAZKZYKYIXRYLLXSYMLYIXRXIUCZXIVDZDHZYLXO
      YODXOXIYNXIVEVGULYIYPYNDHZXLRVAZYLYIYNFITVHZGZXIYSGZYNXIVHZMLZYPYRLYHYTXN
      XIVISYHUUAXNXIVJSZYHUUCXNYHUUBXIYNVHZMYNXIVKYHXIVLUUEMLXIVOXIVMQVPSABYNXI
      DEVNVQYIYQXKXLRYHYQXKLXNABXIDEUDSVRVSVPXRYLPQYIXKFGZXLFGZYKYMLYIXJTGZUUFY
      HUUHXNYHXITGUUHFTXIFVTTVHTWAVTTWBWCWFXIWDWGSXJWEQYIUUAUUGUUDYSFXIDABDEWHW
      IQXKXLWJWKWLWMWNWOVS $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem15 $p |- ( ( ( A e. ( ~P om i^i Fin ) /\
          B e. ( ~P om i^i Fin ) ) /\ ( c e. om /\ c e. A /\ -. c e. B ) ) ->
        -. ( F ` ( A i^i suc c ) ) = ( F ` ( B i^i suc c ) ) ) $=
      ( com cin wcel wa wn csuc cfv wne wceq wpss wss syl syl2anc cpw cv simpr1
      cfn w3a ackbij1lem3 simpr3 ackbij1lem1 inss2 a1i eqsstrd ackbij1lem12 csn
      con0 ackbij1lem10 ffvelrni nnon 3syl onpsssuc ackbij1lem14 psseq2d mpbird
      simpll inss1 ackbij1lem11 cun ssun1 ackbij1lem2 syl5sseqr psssstr sspsstr
      simpr2 df-pss simprbi necomd df-ne sylib ) CHUAUDIZJZDVRJZKZFUBZHJZWBCJZW
      BDJLZUEZKZCWBMZIZENZDWHIZENZOWJWLPLWGWLWJWGWLWJQZWLWJOZWGWLWBENZRZWOWJQZW
      MWGWBVRJZWKWBRWPWGWCWRWAWCWDWEUCZWBUFSZWGWKDWBIZWBWGWEWKXAPWAWCWDWEUGWBDU
      HSXAWBRWGDWBUIUJUKABWKWBEGULTWGWOWBUMZENZQZXCWJRZWQWGXDWOWOMZQZWGWOUNJZXG
      WGWRWOHJXHWTVRHWBEABEGUOUPWOUQURWOUSSWGXCXFWOWGWCXCXFPWSABWBEGUTSVAVBWGWI
      VRJZXBWIRXEWGVSWICRZXIVSVTWFVCXJWGCWHVDUJABCWIEGVETWGXBCWBIZVFZXBWIXBXKVG
      WGWDWIXLPWAWCWDWEVLWBCVHSVIABXBWIEGULTWOXCWJVJTWLWOWJVKTWMWLWJRWNWLWJVMVN
      SVOWJWLVPVQ $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem16 $p |- ( ( A e. ( ~P om i^i Fin ) /\ B e. ( ~P om i^i Fin ) )
        ->
        ( ( F ` A ) = ( F ` B ) -> A = B ) ) $=
      ( com cfn cin wcel cfv wceq wi wss syl c0 ineq2 fveq2d eqeq12d w3a va cpw
      vb cun cuni csuc inss1 sseli elpwi adantr adantl unssd inss2 unfi nnunifi
      wa syl2an syl2anc peano2 cv imbi12d imbi2d weq in0 eqtr4i a1i12 co simp13
      csn coa 3simpa ackbij1lem2 3ad2ant2 ackbij1lem4 simprl ackbij1lem11 incom
      sylancl word nnord orddisj ssdisj sylancr syl5eq syl3anc 3ad2ant1 syl3an1
      ackbij1lem9 eqtrd 3ad2ant3 simprr 3eqtr3d wb ackbij1lem10 ffvelrni nnacan
      3adant3 mpbid uneq2 ad2antrr ad2antlr 3eqtr4d 3adant1 embantd 3exp eqcomd
      ex wn simp12r simp12l simp11 simp3 simp2 syl23anc pm2.65i pm2.21i pm2.61d
      ackbij1lem15 ackbij1lem1 biimpd mpd biimprd com34 finds mpcom con0 omsson
      a2d ssun1 syl6ss onsucuni syl5ss df-ss sylib ssun2 ) CGUBZHIZJZDYQJZUPZCC
      DUDZUEZUFZIZEKZDUUCIZEKZLZUUDUUFLZMZCEKZDEKZLZCDLZMUUCGJZYTUUJYTUUBGJZUUO
      YTUUAGNUUAHJZUUPYTCDGYRCGNZYSYRCYPJUURYQYPCYPHUGZUHCGUIOUJYSDGNZYRYSDYPJU
      UTYQYPDUUSUHDGUIOUKULZYRCHJDHJUUQYSYQHCYPHUMZUHYQHDUVBUHCDUNUQUUAUOURUUBU
      SOYTCUAUTZIZEKZDUVCIZEKZLZUVDUVFLZMZMYTCPIZEKZDPIZEKZLZUVKUVMLZMZMYTCUCUT
      ZIZEKZDUVRIZEKZLZUVSUWALZMZMYTCUVRUFZIZEKZDUWFIZEKZLZUWGUWILZMZMYTUUJMUAU
      CUUCUVCPLZUVJUVQYTUWNUVHUVOUVIUVPUWNUVEUVLUVGUVNUWNUVDUVKEUVCPCQZRUWNUVFU
      VMEUVCPDQZRSUWNUVDUVKUVFUVMUWOUWPSVAVBUAUCVCZUVJUWEYTUWQUVHUWCUVIUWDUWQUV
      EUVTUVGUWBUWQUVDUVSEUVCUVRCQZRUWQUVFUWAEUVCUVRDQZRSUWQUVDUVSUVFUWAUWRUWSS
      VAVBUVCUWFLZUVJUWMYTUWTUVHUWKUVIUWLUWTUVEUWHUVGUWJUWTUVDUWGEUVCUWFCQZRUWT
      UVFUWIEUVCUWFDQZRSUWTUVDUWGUVFUWIUXAUXBSVAVBUVCUUCLZUVJUUJYTUXCUVHUUHUVIU
      UIUXCUVEUUEUVGUUGUXCUVDUUDEUVCUUCCQZRUXCUVFUUFEUVCUUCDQZRSUXCUVDUUDUVFUUF
      UXDUXESVAVBYTUVOUVPUVKPUVMCVDDVDVEVFUVRGJZYTUWEUWMUXFYTUWKUWEUWLUXFYTUWKU
      WEUWLMZUXFYTUWKTZUVRDJZUXGUXHUVRCJZUXIUXGMUXHUXJUXIUXGUXHUXJUXITZUWCUWDUW
      LUXKUVRVIZEKZUVTVJVGZUXMUWBVJVGZLZUWCUXKUWHUWJUXNUXOUXFYTUWKUXJUXIVHUXHUX
      FYTUPZUXJUXIUWHUXNLUXFYTUWKVKZUXQUXJUXITZUWHUXLUVSUDZEKZUXNUXJUXQUWHUYALU
      XIUXJUWGUXTEUVRCVLZRVMUXQUXJUYAUXNLZUXIUXQUXLYQJZUVSYQJZUXLUVSIZPLUYCUXFU
      YDYTUVRVNUJZUXQYRUVSCNUYEUXFYRYSVOCUVRUGABCUVSEFVPVRZUXQUYFUVSUXLIZPUXLUV
      SVQUXQUVSUVRNUVRUXLIPLZUYIPLCUVRUMUXFUYJYTUXFUVRVSUYJUVRVTUVRWAOUJZUVSUVR
      UXLWBWCWDABUXLUVSEFWHWEWFWIWGUXHUXQUXJUXIUWJUXOLUXRUXSUWJUXLUWAUDZEKZUXOU
      XIUXQUWJUYMLUXJUXIUWIUYLEUVRDVLZRWJUXQUXJUYMUXOLZUXIUXQUYDUWAYQJZUXLUWAIZ
      PLUYOUYGUXQYSUWADNUYPUXFYRYSWKDUVRUGABDUWAEFVPVRZUXQUYQUWAUXLIZPUXLUWAVQU
      XQUWAUVRNUYJUYSPLDUVRUMUYKUWAUVRUXLWBWCWDABUXLUWAEFWHWEWFWIWGWLUXHUXJUXPU
      WCWMZUXIUXFYTUYTUWKUXQUXMGJZUVTGJZUWBGJZUYTUXQUYDVUAUYGYQGUXLEABEFWNZWOOU
      XQUYEVUBUYHYQGUVSEVUDWOOUXQUYPVUCUYRYQGUWAEVUDWOOUXMUVTUWBWPWEWQWFWRUXJUX
      IUWDUWLMZUXHUXJUXIUPZUWDUWLVUFUWDUPUXTUYLUWGUWIUWDUXTUYLLVUFUVSUWAUXLWSUK
      UXJUWGUXTLUXIUWDUYBWTUXIUWIUYLLUXJUWDUYNXAXBXGXCXDXEUXHUXJXHZUXIUXGUXHVUG
      UXITZUXGVUHUWJUWHLZVUHUWHUWJUXFYTUWKVUGUXIVHXFVUHYSYRUXFUXIVUGVUIXHYRYSUX
      FUWKVUGUXIXIYRYSUXFUWKVUGUXIXJUXFYTUWKVUGUXIXKUXHVUGUXIXLUXHVUGUXIXMABDCE
      UCFXRXNXOXPXEXQUXHUXJUXIXHZUXGMUXHUXJVUJUXGUXHUXJVUJTZUXGVUKUWKUXFYTUWKUX
      JVUJVHVUKYRYSUXFUXJVUJUWKXHYRYSUXFUWKUXJVUJXJYRYSUXFUWKUXJVUJXIUXFYTUWKUX
      JVUJXKUXHUXJVUJXMUXHUXJVUJXLABCDEUCFXRXNXOXPXEUXHVUGVUJUXGUXHVUGVUJTZUWCU
      WDUWLVULUWKUWCUXFYTUWKVUGVUJVHVUGVUJUWKUWCMUXHVUGVUJUPZUWKUWCVUMUWHUVTUWJ
      UWBVUMUWGUVSEVUGUWGUVSLVUJUVRCXSUJZRVUMUWIUWAEVUJUWIUWALVUGUVRDXSUKZRSXTX
      CYAVUGVUJVUEUXHVUMUWLUWDVUMUWGUVSUWIUWAVUNVUOSYBXCXDXEXQXQXEYCYHYDYEYTUUH
      UUMUUIUUNYTUUEUUKUUGUULYTUUDCEYTCUUCNUUDCLYTCUUAUUCCDYIYTUUAYFNUUAUUCNYTU
      UAGYFUVAYGYJUUAYKOZYLCUUCYMYNZRYTUUFDEYTDUUCNUUFDLYTDUUAUUCDCYOVUPYLDUUCY
      MYNZRSYTUUDCUUFDVUQVURSVAWR $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem17 $p |- F : ( ~P om i^i Fin ) -1-1-> om $=
      ( va vb com cpw cfn cin wf1 wf cv cfv wceq weq wi wral dff13 ackbij1lem10
      ackbij1lem16 rgen2a mpbir2an ) GHIJZGCKUDGCLEMZCNFMZCNOEFPQZFUDREUDREFUDG
      CSABCDTUGEFUDABUEUFCDUAUBUC $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij1 . $)
    ackbij1lem18 $p |- ( A e. ( ~P om i^i Fin ) -> E. b e. ( ~P om i^i Fin )
          ( F ` b ) = suc ( F ` A ) ) $=
      ( com cfn cin wcel cun cfv csuc wceq wss c0 wn sylancr syl coa va cpw csn
      cdif cint wrex difss ackbij1lem11 mpan2 con0 wne omsson sstri ominf inss2
      cv sseli difinf 0fin eleq1 mpbiri necon3bi sseldi ackbij1lem4 ackbij1lem6
      onint syl2anc eldifn disjsn sylibr ssdisj ackbij1lem9 ackbij1lem14 oveq2d
      co syl3anc ackbij1lem10 ffvelrni ackbij1lem3 nnasuc incom eqtri a1i uncom
      disjdif wa wo onnmin mpan con2i adantl word ordom ordelss sselda simplbi2
      eldif orrd orcomd orel1 sylc ssrdv undif sylib syl5eq fveq2d eqtr3d suceq
      ex eqtrd 3eqtrd fveq2 eqeq1d rcla4ev ) CGUBZHIZJZCGCUDZUEZUDZXSUCZKZXPJZY
      BDLZCDLZMZNZEUPZDLZYFNZEXPUFXQXTXPJZYAXPJZYCXQXTCOZYKCXSUGZABCXTDFUHUIZXQ
      XSGJZYLXQXRGXSGCUGZXQXRUJOZXRPUKZXSXRJZXRGUJYQULUMZXQXRHJZQZYSXQGHJQCHJUU
      CUNXPHCXOHUOUQGCURRUUBXRPXRPNUUBPHJUSXRPHUTVAVBSXRVFRZVCZXSVDSZXTYAVEVGXQ
      YDXTDLZYADLZTVOZUUGXSDLZMZTVOZYFXQYKYLXTYAIPNZYDUUINYOUUFXQYMCYAIPNZUUMYN
      XQXSCJQZUUNXQYTUUOUUDXSGCVHSCXSVIVJXTCYAVKRABXTYADFVLVPXQUUHUUKUUGTXQYPUU
      HUUKNUUEABXSDFVMSVNXQUULUUGUUJTVOZMZYFXQUUGGJZUUJGJZUULUUQNXQYKUURYOXPGXT
      DABDFVQZVRSXQXSXPJZUUSXQYPUVAUUEXSVSSZXPGXSDUUTVRSUUGUUJVTVGXQUUPYENUUQYF
      NXQXTXSKZDLZUUPYEXQYKUVAXTXSIZPNZUVDUUPNYOUVBUVFXQUVEXSXTIPXTXSWAXSCWEWBW
      CABXTXSDFVLVPXQUVCCDXQUVCXSXTKZCXTXSWDXQXSCOUVGCNXQUAXSCXQUAUPZXSJZUVHCJZ
      XQUVIWFZUVHXRJZQZUVLUVJWGZUVJUVIUVMXQUVLUVIYRUVLUVIQUUAXRUVHWHWIWJWKUVKUV
      HGJZUVNXQXSGUVHXQGWLYPXSGOWMUUEGXSWNRWOUVOUVJUVLUVOUVJUVLUVLUVOUVJQUVHGCW
      QWPWRWSSUVLUVJWTXAXIXBXSCXCXDXEXFXGUUPYEXHSXJXKYJYGEYBXPYHYBNYIYDYFYHYBDX
      LXMXNVG $.
      $( [18-Nov-2014] $)

    $( The Ackermann bijection, part 1: each natural number can be uniquely
       coded in binary as a finite set of natural numbers and conversely.
       (Contributed by Stefan O'Rear, 18-Nov-2014.) $)
    ackbij1 $p |- F : ( ~P om i^i Fin ) -1-1-onto-> om $=
      ( va vb vc com cpw wceq cv wcel c0 csuc eleq1 cfv wrex ax-mp wb fvelrnb
      cfn cin wf1o wf1 crn dff1o5 ackbij1lem17 wf wss f1f frn mp2b ackbij1lem13
      peano1 ackbij1lem3 fveq2 eqeq1d rcla4ev mp2an wfn f1fn mpbir ackbij1lem18
      adantl suceq eqeq2d rexbidv syl5ibcom rexlimdva 3imtr4g finds ssriv eqssi
      wa mpbir2an ) HIUAUBZHCUCVPHCUDZCUEZHJVPHCUFABCDUGZVRHVQVPHCUHVRHUIVSVPHC
      UJVPHCUKULEHVRFKZVRLMVRLZEKZVRLZWBNZVRLZWCFEWBVTMVROVTWBVROZVTWDVROWFWAWB
      CPZMJZEVPQZMVPLZMCPZMJZWIMHLWJUNMUORABCDUMWHWLEMVPWBMJWGWKMWBMCUPUQURUSCV
      PUTZWAWISVQWMVSVPHCVARZEVPMCTRVBWBHLZGKZCPZWBJZGVPQZVTCPZWDJZFVPQZWCWEWOW
      RXBGVPWOWPVPLZVNWTWQNZJZFVPQZWRXBXCXFWOABWPCFDVCVDWRXEXAFVPWRXDWDWTWQWBVE
      VFVGVHVIWMWCWSSWNGVPWBCTRWMWEXBSWNFVPWDCTRVJVKVLVMVO $.
      $( [18-Nov-2014] $)

    $( The Ackermann bijection, part 1b: the bijection from ~ ackbij1 restricts
       naturally to the powers of particular naturals.  (Contributed by Stefan
       O'Rear, 18-Nov-2014.) $)
    ackbij1b $p |- ( A e. om -> ( F " ~P A ) = ( card ` ~P A ) ) $=
      ( va com wcel cpw cima cfv wn wceq cen wbr cfn wss sylib syl ax-mp wo cin
      ccrd wpss cv imaeq2d breq12d wf1 ackbij1lem17 ackbij2lem1 f1imaen sylancr
      pweq vex pwex vtoclga con0 onfin2 inss2 eqsstri pwfi ficardid ensymg sylc
      sseli entr syl2anc csdm wi ficardom sseldi php3 ex sdomnen syl6 mt2d wral
      cvv wa csuc fvex ackbij1lem3 ackbij1lem12 syl2an word ackbij1lem10 peano1
      elpwi f0cli nnord ordsucsssuc csn ackbij1lem14 ackbij1lem8 eqtr3d sseqtrd
      wb mp2an adantr sucssel mpsyl ralrimiva wfun cdm f1dm syl6sseqr funimass4
      f1fun mpbird sspss orel1 ) CGHZDCIZJZXMUCKZUDZLXPXNXOMZUAZXQXLXPXNXONOZXL
      XNXMNOZXMXONOZXSDFUEZIZJZYCNOZXTFCGYBCMZYDXNYCXMNYFYCXMDYBCUMZUFYGUGYBGHG
      IPUBZGDUHZYCYHQYEABDEUIZYBUJYHGYCDYBFUNUOUKULUPXLXMPHZXOXMNOZYAXLCPHYKGPC
      GUQPUBPURUQPUSUTZVECVARZXLYKYLYNXMVBSXOXMPVCVDXNXMXOVFVGXLXPXNXOVHOZXSLXL
      XOPHZXPYOVIXLGPXOYMXLYKXOGHYNXMVJSVKYPXPYOXOXNVLVMSXNXOVNVOVPXLXNXOQZXRXL
      YQYBDKZXOHZFXMVQZXLYSFXMYRVRHXLYBXMHZVSZYRVTZXOQYSYBDWAUUBUUCCDKZVTZXOUUB
      YRUUDQZUUCUUEQZXLCYHHYBCQUUFUUACWBYBCWHABYBCDEWCWDYRWEZUUDWEZUUFUUGWQYRGH
      UUHYHGYBDABDEWFZWGWIYRWJTUUDGHUUIYHGCDUUJWGWIUUDWJTYRUUDWKWRRXLUUEXOMUUAX
      LCWLDKUUEXOABCDEWMABCDEWNWOWSWPYRXOVRWTXAXBXLDXCZXMDXDZQYQYTWQYIUUKYJYHGD
      XHTXLXMYHUULCUJYIUULYHMYJYHGDXETXFFXMXODXGULXIXNXOXJRXPXQXKVD $.
      $( [18-Nov-2014] $)

    ackbij.g $e |- G = ( x e. _V |-> ( y e. ~P dom x |->
          ( F ` ( x " y ) ) ) ) $.
    $( Lemma for ~ ackbij2 . $)
    ackbij2lem2 $p |- ( A e. om -> ( rec ( G , (/) ) ` A ) :
        ( R1 ` A ) -1-1-onto-> ( card ` ( R1 ` A ) ) ) $=
      ( cr1 cfv ccrd c0 wf1o wceq wb fveq2 syl fveq2d com wcel cvv va crdg csuc
      vb vc cv f1oeq1 f1oeq23 syl2anc bitrd weq f1o0 0ex ax-mp r10 fveq2i card0
      rdg0 eqtri mp2an bitri mpbir cpw cres cima cmpt ccom cfn cin ackbij1lem17
      wa wf1 wss a1i ficardom ackbij2lem1 f1ores ackbij1b cen wbr ficardid fvex
      r1fin pwen pwex carden2b eqtrd f1oeq3 mpbid adantr f1opw adantl f1oco cdm
      wi 3syl frsuc peano2 fvres dmeq pweqd imaeq1 mpteq12dv mptex fvmpt syl6eq
      dmex 3eqtr3d f1odm mpteq1 wfn eqid fnmpt mprg f1ofn wf f1of ffvelrn sylan
      imaeq2 imaexg fvco4 3eqtr4rd eqfnfvd 3eqtrd con0 nnon r1suc mpbird finds
      ex ) UAUFZHIZYMJIZYLEKUBZIZLZKHIZYRJIZKYOIZLZUDUFZHIZUUCJIZUUBYOIZLZUUBUC
      ZHIZUUHJIZUUGYOIZLZCHIZUULJIZCYOIZLZUAUDCYLKMZYQYMYNYTLZUUAUUPYPYTMYQUUQN
      YLKYOOYMYNYPYTUGPUUPYMYRMYNYSMUUQUUANYLKHOZUUPYMYRJUURQYMYRYNYSYTUHUIUJUA
      UDUKZYQYMYNUUELZUUFUUSYPUUEMYQUUTNYLUUBYOOYMYNYPUUEUGPUUSYMUUCMYNUUDMUUTU
      UFNYLUUBHOZUUSYMUUCJUVAQYMUUCYNUUDUUEUHUIUJYLUUGMZYQYMYNUUJLZUUKUVBYPUUJM
      YQUVCNYLUUGYOOYMYNYPUUJUGPUVBYMUUHMYNUUIMUVCUUKNYLUUGHOZUVBYMUUHJUVDQYMUU
      HYNUUIUUJUHUIUJYLCMZYQYMYNUUNLZUUOUVEYPUUNMYQUVFNYLCYOOYMYNYPUUNUGPUVEYMU
      ULMYNUUMMUVFUUONYLCHOZUVEYMUULJUVGQYMUULYNUUMUUNUHUIUJUUAKKKLZULUUAYRYSKL
      ZUVHYTKMUUAUVINKEUMURYRYSYTKUGUNYRKMYSKMUVIUVHNUOYSKJIKYRKJUOUPUQUSYRKYSK
      KUHUTVAVBUUBRSZUUFUUKUVJUUFVKZUUKUUCVCZUVLJIZDUUDVCZVDZUAUVLUUEYLVEZVFZVG
      ZLZUVKUVNUVMUVOLZUVLUVNUVQLZUVSUVJUVTUUFUVJUVNDUVNVEZUVOLZUVTUVJRVCVHVIZR
      DVLZUVNUWDVMZUWCUWEUVJABDFVJVNUVJUUDRSZUWFUVJUUCVHSZUWGUUBWCZUUCVOPZUUDVP
      PUWDRUVNDVQUIUVJUWBUVMMUWCUVTNUVJUWBUVNJIZUVMUVJUWGUWBUWKMUWJABUUDDFVRPUV
      JUUDUUCVSVTZUVNUVLVSVTZUWKUVMMZUVJUWHUWLUWIUUCWAPUUDUUCUUBHWBZWDUVLTSUWMU
      WNWOUUCUWOWEUVNUVLTWFUNWPWGUWBUVMUVNUVOWHPWIWJUUFUWAUVJUUCUUDUUEUAWKWLZUV
      LUVNUVMUVOUVQWMUIZUVKUUKUUHUUIUVRLZUVSUVKUUJUVRMUUKUWRNUVKUUJBUUEWNZVCZUU
      EBUFZVEZDIZVFZBUVLUXCVFZUVRUVJUUJUXDMUUFUVJUUGYORVDZIZUUBUXFIZEIZUUJUXDKU
      UBEWQUVJUUGRSUXGUUJMUUBWRUUGRYOWSPUVJUXIUUEEIZUXDUVJUXHUUEEUUBRYOWSQUUETS
      ZUXJUXDMUUBYOWBZAUUEBAUFZWNZVCZUXMUXAVEZDIZVFUXDTEUXMUUEMZBUXOUXQUWTUXCUX
      RUXNUWSUXMUUEWTXAUXRUXPUXBDUXMUUEUXAXBQXCGBUWTUXCUWSUUEUXLXGWEXDXEUNXFXHW
      JUVKUWTUVLMUXDUXEMUVKUWSUUCUUFUWSUUCMUVJUUCUUDUUEXIWLXABUWTUVLUXCXJPUVKUE
      UVLUXEUVRUXEUVLXKZUVKUXCTSZUXSBUVLBUVLUXCUXETUXEXLZXMUXTUXAUVLSUXBDWBVNXN
      VNUVKUVSUVRUVLXKUWQUVLUVMUVRXOPUVKUEUFZUVLSZVKZUYBUVQIZUVOIZUUEUYBVEZDIZU
      YBUVRIZUYBUXEIZUYDUYFUYEDIZUYHUYDUYEUVNSZUYFUYKMUVKUVLUVNUVQXPZUYCUYLUVKU
      WAUYMUWPUVLUVNUVQXQPUVLUVNUYBUVQXRXSUYEUVNDWSPUYDUYEUYGDUYCUYEUYGMUVKUAUY
      BUVPUYGUVLUVQYLUYBUUEXTUVQXLUXKUYGTSUXLUUEUYBTYAUNXEWLQWGUVKUVQUVLXKZUYCU
      YIUYFMUVKUWAUYNUWPUVLUVNUVQXOPUVLUVOUVQUYBYBXSUYCUYJUYHMUVKBUYBUXCUYHUVLU
      XEBUEUKUXBUYGDUXAUYBUUEXTQUYAUYGDWBXEWLYCYDYEUUHUUIUUJUVRUGPUVJUWRUVSNZUU
      FUVJUUHUVLMZUUIUVMMUYOUVJUUBYFSUYPUUBYGUUBYHPZUVJUUHUVLJUYQQUUHUVLUUIUVMU
      VRUHUIWJUJYIYKYJ $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij2lem3 $p |- ( A e. om -> ( rec ( G , (/) ) ` A ) C_
        ( rec ( G , (/) ) ` suc A ) ) $=
      ( wcel c0 cfv csuc cr1 cres cv wceq fveq2 fveq2d syl wss cima va com crdg
      vb suceq reseq12d eqeq12d weq res0 r10 reseq2i 0ex rdg0 3eqtr4ri wfn ccrd
      vc wf1o peano2 ackbij2lem2 f1ofn adantr con0 nnon r1sssuc fnssres syl2anc
      wa fveq1d ad2antlr cpw r1suc eleq2d biimpa vex elpw sylib resima2 cdm cvv
      cmpt fvex resex dmeq pweqd imaeq1 mpteq12dv dmex mptex fvmpt ax-mp fveq1i
      pwex fndm eleqtrrd imaeq2 eqid syl5eq wtr r1tr dftr4 sselda f1odm 3eqtr4d
      a1i adantlr eqtrd rdgsuc ad2antrr fvres adantl eqtr4d eqfnfvd finds resss
      ex eqsstrd ) CUBHZCEIUCZJZCKZXSJZCLJZMZYBUANZXSJZYEKZXSJZYELJZMZOIXSJZIKZ
      XSJZILJZMZOUDNZXSJZYPKZXSJZYPLJZMZOZYSYRKZXSJZYRLJZMZOZXTYDOUAUDCYEIOZYFY
      KYJYOYEIXSPUUHYHYMYIYNUUHYGYLXSYEIUEQYEILPUFUGUAUDUHZYFYQYJUUAYEYPXSPUUIY
      HYSYIYTUUIYGYRXSYEYPUEQYEYPLPUFUGYEYROZYFYSYJUUFYEYRXSPUUJYHUUDYIUUEUUJYG
      UUCXSYEYRUEQYEYRLPUFUGYECOZYFXTYJYDYECXSPUUKYHYBYIYCUUKYGYAXSYECUEQYECLPU
      FUGYMIMIYOYKYMUIYNIYMUJUKIEULUMUNYPUBHZUUBUUGUULUUBVHZUQUUEYSUUFUULYSUUEU
      OZUUBUULUUEUUEUPJZYSURZUUNUULYRUBHZUUPYPUSZABYRDEFGUTRZUUEUUOYSVARZVBUULU
      UFUUEUOZUUBUULUUDUUCLJZUOZUUEUVBSZUVAUULUVBUVBUPJZUUDURZUVCUULUUCUBHZUVFU
      ULUUQUVGUURYRUSRABUUCDEFGUTRUVBUVEUUDVARUULYRVCHZUVDUULUUQUVHUURYRVDRZYRV
      ERUVBUUEUUDVFVGVBUUMUQNZUUEHZVHZUVJYSJZUVJUUDJZUVJUUFJZUVLUVJYQEJZJZUVJYS
      EJZJZUVMUVNUVLUVQUVJUUAEJZJZUVSUUBUVQUWAOUULUVKUUBUVJUVPUVTYQUUAEPVIVJUUL
      UVKUWAUVSOUUBUULUVKVHZUUAUVJTZDJZYSUVJTZDJZUWAUVSUWBUWCUWEDUWBUVJYTSZUWCU
      WEOUWBUVJYTVKZHZUWGUULUVKUWIUULUUEUWHUVJUULYPVCHZUUEUWHOYPVDZYPVLRVMVNZUV
      JYTUQVOVPVQYSUVJYTVRRQUWBUWAUVJBUUAVSZVKZUUABNZTZDJZWAZJZUWDUVJUVTUWRUUAV
      THUVTUWROYSYTYRXSWBZWCZAUUABANZVSZVKZUXBUWOTZDJZWAZUWRVTEUXBUUAOZBUXDUXFU
      WNUWQUXHUXCUWMUXBUUAWDWEUXHUXEUWPDUXBUUAUWOWFQWGGBUWNUWQUWMUUAUXAWHWMWIWJ
      WKWLUWBUVJUWNHUWSUWDOUWBUVJUWHUWNUWLUULUWNUWHOUVKUULUWMYTUULUUAYTUOZUWMYT
      OUULUUNYTUUESZUXIUUTUULUWJUXJUWKYPVERUUEYTYSVFVGYTUUAWNRWEVBWOBUVJUWQUWDU
      WNUWRBUQUHZUWPUWCDUWOUVJUUAWPQUWRWQUWCDWBWJRWRUWBUVSUVJBYSVSZVKZYSUWOTZDJ
      ZWAZJZUWFUVJUVRUXPYSVTHUVRUXPOUWTAYSUXGUXPVTEUXBYSOZBUXDUXFUXMUXOUXRUXCUX
      LUXBYSWDWEUXRUXEUXNDUXBYSUWOWFQWGGBUXMUXOUXLYSUWTWHWMWIWJWKWLUWBUVJUXMHUX
      QUWFOUWBUVJUUEVKZUXMUULUUEUXSUVJUULUUEWSZUUEUXSSUXTUULYRWTXEUUEXAVQXBUULU
      XMUXSOUVKUULUXLUUEUULUUPUXLUUEOUUSUUEUUOYSXCRWEVBWOBUVJUXOUWFUXMUXPUXKUXN
      UWEDUWOUVJYSWPQUXPWQUWEDWBWJRWRXDXFXGUULUVMUVQOUUBUVKUULUVJYSUVPUULUWJYSU
      VPOUWKIYPEXHRVIXIUULUVNUVSOUUBUVKUULUVJUUDUVRUULUVHUUDUVROUVIIYREXHRVIXIX
      DUVKUVOUVNOUUMUVJUUEUUDXJXKXLXMXPXNYDYBSXRYBYCXOXEXQ $.
      $( [18-Nov-2014] $)

    $( Lemma for ~ ackbij2 . $)
    ackbij2lem4 $p |- ( ( ( A e. om /\ B e. om ) /\ B C_ A ) ->
        ( rec ( G , (/) ) ` B ) C_ ( rec ( G , (/) ) ` A ) ) $=
      ( va vb c0 cfv cv wss wceq fveq2 sseq2d com wcel wa crdg csuc ackbij2lem3
      weq ssid a1i ad2antrr sstr2 syl5com findsg ) DFKUAZLZIMZUKLZNULULNZULJMZU
      KLZNZULUPUBZUKLZNZULCUKLZNIJCDUMDOUNULULUMDUKPQIJUDUNUQULUMUPUKPQUMUSOUNU
      TULUMUSUKPQUMCOUNVBULUMCUKPQUODRSZULUEUFUPRSZVCTDUPNZTUQUTNZURVAVDVFVCVEA
      BUPEFGHUCUGULUQUTUHUIUJ $.
      $( [18-Nov-2014] $)

    ackbij.h $e |- H = U. ( rec ( G , (/) ) " om ) $.
    $( The Ackermann bijection, part 2: hereditarily finite sets can be
       represented by recursive binary notation.  (Contributed by Stefan
       O'Rear, 18-Nov-2014.) $)
    ackbij2 $p |- H : U. ( R1 " om ) -1-1-onto-> om $=
      ( va vb cr1 com wf1o wceq cfv wss wcel cfn con0 ax-mp vc cima cuni c0 wf1
      crdg dff1o5 cv ciun wo wral wa fveq2 fvex fun11iun ccrd ackbij2lem2 f1of1
      crn syl r1fin ficardom word ordom ordelss mpan 3syl f1ss nnord ordtri2or2
      syl2anc syl2an wi ackbij2lem4 ex ancoms orim12d mpd ralrimiva jca mprg wb
      wfun wfn rdgfnon fnfun funiunfv eqcomd f1eq1 mp2b f1eq2 bitri mpbir rnuni
      r1fnon wrex wex eliun df-rex omsson fvelimab mp2an wfo f1ofo forn sylancr
      eqsstrd rneq sseq1d syl5ibcom rexlimiv sylbi sselda exlimiv csuc cdm fndm
      peano2 sseqtr4i funfvima2 cardnn cdom wbr onssr1 cvv vex sucex ssdomg cin
      nnon onfin2 inss2 eqsstri sseli ficarddom mpbid eqsstr3d sucssel eleqtrrd
      eleq1 eleq2d anbi12d cla4ev impbii 3bitri eqriv eqtri mpbir2an f1oeq1 ) K
      LUBUCZLEMZUUJLDUDUFZLUBZUCZMZUUOUUJLUUNUEZUUNUSZLNUUJLUUNUGUUPILIUHZKOZUI
      ZLILUURUULOZUIZUEZUUSLUVAUEZUVAJUHZUULOZPZUVFUVAPZUJZJLUKZULUVCILIJLUVAUV
      FUUSLUURUVEUULUMUURUULUNUOUURLQZUVDUVJUVKUUSUUSUPOZUVAUEZUVLLPZUVDUVKUUSU
      VLUVAMUVMABUURCDFGUQUUSUVLUVAURUTUVKUUSRQUVLLQZUVNUURVAUUSVBLVCZUVOUVNVDL
      UVLVEVFVGUUSUVLLUVAVHVKUVKUVIJLUVKUVELQZULZUURUVEPZUVEUURPZUJZUVIUVKUURVC
      UVEVCUWAUVQUURVIUVEVIUURUVEVJVLUVRUVSUVGUVTUVHUVQUVKUVSUVGVMUVQUVKULUVSUV
      GABUVEUURCDFGVNVOVPUVRUVTUVHABUURUVECDFGVNVOVQVRVSVTWAUUPUUJLUVBUEZUVCUUL
      WCZUUNUVBNUUPUWBWBUULSWDZUWCUDDWEZSUULWFTZUWCUVBUUNILUULWGWHUUJLUUNUVBWIW
      JKWCZUUJUUTNUWBUVCWBKSWDUWGWOSKWFTUWGUUTUUJILKWGWHUUJUUTLUVBWKWJWLWMUUQIU
      UMUURUSZUIZLIUUMWNJUWILUVEUWIQUVEUWHQZIUUMWPUURUUMQZUWJULZIWQZUVQIUVEUUMU
      WHWRUWJIUUMWSUWMUVQUWLUVQIUWKUWHLUVEUWKUAUHZUULOZUURNZUALWPZUWHLPZUWDLSPU
      WKUWQWBUWEWTUASLUURUULXAXBUWPUWRUALUWNLQZUWOUSZLPUWPUWRUWSUWTUWNKOZUPOZLU
      WSUXAUXBUWOMUXAUXBUWOXCUWTUXBNABUWNCDFGUQUXAUXBUWOXDUXAUXBUWOXEVGUWSUVPUX
      BLQZUXBLPVDUWSUXARQUXCUWNVAUXAVBUTLUXBVEXFXGUWPUWTUWHLUWOUURXHXIXJXKXLXMX
      NUVQUVEXOZUULOZUUMQZUVEUXEUSZQZUWMUVQUXDLQZUXFUVEXRZUWCLUULXPZPUXIUXFVMUW
      FLSUXKWTUWDUXKSNUWESUULXQTXSLUXDUULXTXBUTUVQUVEUXDKOZUPOZUXGUVQUXIUXDUXMP
      ZUVEUXMQZUXJUXIUXDUXDUPOZUXMUXDYAUXIUXDUXLYBYCZUXPUXMPZUXIUXDSQUXDUXLPZUX
      QUXDYJUXDYDUXDYEQUXSUXQVMUVEJYFZYGUXDUXLYEYHTVGUXIUXDRQUXLRQUXQUXRWBLRUXD
      LSRYIRYKSRYLYMYNUXDVAUXDUXLYOVKYPYQUVEYEQUXNUXOVMUXTUVEUXMYEYRTVGUVQUXLUX
      MUXEMZUXLUXMUXEXCUXGUXMNUVQUXIUYAUXJABUXDCDFGUQUTUXLUXMUXEXDUXLUXMUXEXEVG
      YSUWLUXFUXHULIUXEUXDUULUNUURUXENZUWKUXFUWJUXHUURUXEUUMYTUYBUWHUXGUVEUURUX
      EXHUUAUUBUUCVKUUDUUEUUFUUGUUHEUUNNUUKUUOWBHUUJLEUUNUUITWM $.
      $( [18-Nov-2014] $)
  $}

  ${
    $d a b c d e f $.

    $( The set of hereditarily finite sets is countable.  See ~ ackbij2 for an
       explicit bijection that works without Infinity.  (Contributed by Stefan
       O'Rear, 18-Nov-2014.) $)
    r1omNEW $p |- ( R1 ` om ) ~~ om $=
      ( vc vd ve vf va vb com cvv cr1 cfv cen cv cpw cima ciun ccrd cmpt syl6eq
      weq fveq2d wcel wbr cdm cfn cin csn c0 crdg cuni wf1o iuneq1 sneq xpeq12d
      pweq cbviunv cbvmptv dmeq pweqd imaeq1 mpteq12dv imaeq2 eqid ackbij2 wceq
      cxp wb wlim limom r1lim mpan2 con0 wfun r1fnon fnfun funiunfv mp2b f1oeq2
      wfn syl mpbiri fvex f1oen wn enref brprc pm2.61i ) GHUAZGIJZGKUBZWGWHGAHB
      ALZUCZMZWJBLZNZCGMUDUEZDCLZDLZUFZWQMZVEZOZPJZQZJZQZQZUGUHGNUIZUJZWIWGXHIG
      NUIZGXGUJZEFXCXFXGCEWOXBFELZFLZUFZXLMZVEZOZPJCESZXAXPPXQXADXKWTOXPDWPXKWT
      UKDFXKWTXODFSWRXMWSXNWQXLULWQXLUNUMUORTUPAEHXEFXKUCZMZXKXLNZXCJZQZAESZXEB
      XSXKWMNZXCJZQYBYCBWLXDXSYEYCWKXRWJXKUQURYCWNYDXCWJXKWMUSTUTBFXSYEYABFSYDX
      TXCWMXLXKVATUPRUPXGVBVCWGWHXIVDXHXJVFWGWHEGXKIJOZXIWGGVGWHYFVDVHEGHVIVJIV
      KVRIVLYFXIVDVMVKIVNEGIVOVPRWHXIGXGVQVSVTWHGXGGIWAZWBVSWGWCWIWHWHKUBWHYGWD
      WHGKWEVTWF $.
      $( [18-Nov-2014] $)
  $}

$( (End of Stefan O'Rear's mathbox.) $)


