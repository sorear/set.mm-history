$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    The dual of an ordered set
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ODual $.

  $( Class function defining dual orders. $)
  codu $a class ODual $.

  $( Define the dual of an ordered structure, which replaces the order
     component of the structure with its reverse.  See ~ odubas , ~ oduleval ,
     and ~ oduleg for its principal properties.

     _EDITORIAL_: likely usable to simplify many lattice proofs, as it allows
     for duality arguments to be formalized; for instance ~ latmass . $)
  df-odu $a |- ODual = ( w e. _V |->
      ( w sSet <. ( le ` ndx ) , `' ( le ` w ) >. ) ) $.

  ${
    $d a b $.

    $( The structure override operator is a proper operator.  (Contributed by
       Stefan O'Rear, 29-Jan-2015.) $)
    reldmsets $p |- Rel dom sSet $=
      ( va vb cvv cv csn cdm cdif cres cun csts df-sets reldmmpt2 ) ABCCADCBDEZ
      FGHMIJBAKL $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d D a $.  $d L a $.  $d O a $.  $d G a $.  $d A a $.  $d B a $.
    oduval.d $e |- D = ( ODual ` O ) $.

    ${
      oduval.l $e |- L = ( le ` O ) $.
      $( Value of an order dual structure.  (Contributed by Stefan O'Rear,
         29-Jan-2015.) $)
      oduval $p |- D = ( O sSet <. ( le ` ndx ) , `' L >. ) $=
        ( va codu cfv cnx cple ccnv cop csts co cvv wcel wceq cv id fveq2 fvmpt
        cnveqd opeq2d oveq12d df-odu ovex wn c0 reldmsets ovprc1 eqtr4d pm2.61i
        fvprc cnveqi opeq2i oveq2i 3eqtr4i ) CGHZCIJHZCJHZKZLZMNZACUSBKZLZMNCOP
        ZURVCQFCFRZUSVGJHZKZLZMNVCOGVGCQZVGCVJVBMVKSVKVIVAUSVKVHUTVGCJTUBUCUDFU
        ECVBMUFUAVFUGURUHVCCGUMCVBMUIUJUKULDVEVBCMVDVAUSBUTEUNUOUPUQ $.
        $( [29-Jan-2015] $)

      $( Value of the less-equal relation in an order dual structure.
         (Contributed by Stefan O'Rear, 29-Jan-2015.) $)
      oduleval $p |- `' L = ( le ` D ) $=
        ( va cple cfv ccnv cnx cop csts co cvv wcel wceq fvex cnvex pleid c0 wn
        setsid mpan2 str0 cnveqd syl6eq reldmsets ovprc1 fveq2d 3eqtr4a pm2.61i
        fvprc cnv0 cnveqi eqid oduval fveq2i 3eqtr4i ) CGHZIZCJGHZUTKZLMZGHZBIA
        GHCNOZUTVDPZVEUTNOVFUSCGQRNUTGNCFFSZUBUCVEUAZTTGHUTVDGVAFVGUDVHUTTITVHU
        STCGULUEUMUFVHVCTGCVBLUGUHUIUJUKBUSEUNAVCGAUSCDUSUOUPUQUR $.
        $( [29-Jan-2015] $)

      oduleg.g $e |- G = ( le ` D ) $.
      $( Truth of the less-equal relation in an order dual structure.
         (Contributed by Stefan O'Rear, 29-Jan-2015.) $)
      oduleg $p |- ( ( A e. V /\ B e. W ) -> ( A G B <-> B L A ) ) $=
        ( wbr ccnv wcel wa cple cfv oduleval eqtr4i breqi brcnvg syl5bb ) ABDLA
        BEMZLAGNBHNOBAELABDUCDCPQUCKCEFIJRSTABGHEUAUB $.
        $( [29-Jan-2015] $)
    $}

    odubas.b $e |- B = ( Base ` O ) $.
    $( Base set of an order dual structure.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    odubas $p |- B = ( Base ` D ) $=
      ( va cbs cfv cnx cple ccnv cop csts co cvv wcel wceq baseid wn c0 csn wne
      poslem fvex elsnc necon3bbii mpbi necomi setsnid str0 fvprc ovprc1 fveq2d
      reldmsets 3eqtr4a pm2.61i eqid oduval fveq2i 3eqtr4i ) CGHZCIJHZCJHZKZLZM
      NZGHZABGHCOPZVAVGQVDVBGOCFFRZVBIGHZVBVJUAPZSVBVJUBUCVKVBVJVBVJIJUDUEUFUGU
      HUIVHSZTTGHVAVGGVJFVIUJCGUKVLVFTGCVEMUNULUMUOUPEBVFGBVCCDVCUQURUSUT $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d B x y a b c $.  $d ph x y a b c $.  $d K x y a b c $.  $d L x y a b c $.
    pospropd.kv $e |- ( ph -> K e. V ) $.
    pospropd.lv $e |- ( ph -> L e. W ) $.
    pospropd.kb $e |- ( ph -> B = ( Base ` K ) ) $.
    pospropd.lb $e |- ( ph -> B = ( Base ` L ) ) $.
    pospropd.xy $e |- ( ( ph /\ ( x e. B /\ y e. B ) ) ->
        ( x ( le ` K ) y <-> x ( le ` L ) y ) ) $.
    $( Posethood is determined only by structure components and only by the
       value of the relation within the base set.  (Contributed by Stefan
       O'Rear, 29-Jan-2015.) $)
    pospropd $p |- ( ph -> ( K e. Poset <-> L e. Poset ) ) $=
      ( va vb vc wbr wa wral wb cvv wcel cv cple cfv weq w3a cbs cpo ralrimivva
      wi simp1 breq1 bibi12d breq2 rcla42va sylan 3adantl3 3simpb 3comr anbi12d
      jca imbi1d 3adantl1 imbi12d 3anbi123d sylan2 ancoms 3exp2 imp42 2ralbidva
      ralbidva wceq raleq raleqbi1dv 3bitr3d elex biantrurd eqid ispos 3bitr4g
      syl ) AEUAUBZNUCZWDEUDUEZQZWDOUCZWEQZWGWDWEQZRZNOUFZUKZWHWGPUCZWEQZRZWDWM
      WEQZUKZUGZPEUHUEZSZOWSSZNWSSZRZFUAUBZWDWDFUDUEZQZWDWGXEQZWGWDXEQZRZWKUKZX
      GWGWMXEQZRZWDWMXEQZUKZUGZPFUHUEZSZOXPSZNXPSZRZEUIUBFUIUBAXBXSXCXTAWRPDSZO
      DSZNDSZXOPDSZODSZNDSZXBXSAYAYDNODDAWDDUBZWGDUBZRRWRXOPDAYGYHWMDUBZWRXOTZA
      YGYHYIYJYGYHYIUGZAYJAYKBUCZCUCZWEQZYLYMXEQZTZCDSBDSZYJAYPBCDDMUJYKYQRZWFX
      FWLXJWQXNYKYGYGRYQWFXFTZYKYGYGYGYHYIULZYTVBYPYSWDYMWEQZWDYMXEQZTZBCWDWDDD
      BNUFYNUUAYOUUBYLWDYMWEUMYLWDYMXEUMUNZCNUFZUUAWFUUBXFYMWDWDWEUOYMWDWDXEUOU
      NUPUQYRWJXIWKYRWHXGWIXHYGYHYQWHXGTZYIYPUUFUUCBCWDWGDDUUDCOUFUUAWHUUBXGYMW
      GWDWEUOYMWGWDXEUOUNUPURZYKYHYGRZYQWIXHTZYHYIYGUUHYHYIYGUSUTYPUUIWGYMWEQZW
      GYMXEQZTZBCWGWDDDBOUFYNUUJYOUUKYLWGYMWEUMYLWGYMXEUMUNZUUEUUJWIUUKXHYMWDWG
      WEUOYMWDWGXEUOUNUPUQVAVCYRWOXLWPXMYRWHXGWNXKUUGYHYIYQWNXKTZYGYPUUNUULBCWG
      WMDDUUMCPUFZUUJWNUUKXKYMWMWGWEUOYMWMWGXEUOUNUPVDVAYKYGYIRYQWPXMTZYGYHYIUS
      YPUUPUUCBCWDWMDDUUDUUOUUAWPUUBXMYMWMWDWEUOYMWMWDXEUOUNUPUQVEVFVGVHVIVJVLV
      KADWSVMYCXBTKYBXANDWSYAWTODWSWRPDWSVNVOVOWBADXPVMYFXSTLYEXRNDXPYDXQODXPXO
      PDXPVNVOVOWBVPAWCXBAEGUBWCIEGVQWBVRAXDXSAFHUBXDJFHVQWBVRVPNOPWSEWEWSVSWEV
      SVTNOPXPFXEXPVSXEVSVTWA $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d D a b c $.  $d O a b c $.  $d V a b c $.
    odupos.d $e |- D = ( ODual ` O ) $.
    $( Being a poset is a self-dual property.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    odupos $p |- ( O e. Poset -> D e. Poset ) $=
      ( va vb vc wcel cbs cfv cple cvv codu a1i wceq cv wa wbr vex brcnv w3a wi
      cpo ccnv fvex eqeltri eqid odubas oduleval posref weq anbi12i ancom bitri
      sylibr posasymb biimpd syl5bi 3anrev postr sylan2b 3imtr4g isposd ) BUBGZ
      DEFBHIZABJIZUCZAKGVCABLIKCBLUDUEMVDAHINVCVDABCVDUFZUGMVFAJINVCAVEBCVEUFZU
      HMVCDOZVDGZPVIVIVEQVIVIVFQVDBVEVIVGVHUIVIVIVEDRZVKSUNVIEOZVFQZVLVIVFQZPZV
      IVLVEQZVLVIVEQZPZVCVJVLVDGZTZDEUJZVOVQVPPVRVMVQVNVPVIVLVEVKERZSZVLVIVEWBV
      KSUKVQVPULUMVTVRWAVDBVEVIVLVGVHUOUPUQVCVJVSFOZVDGZTZPWDVLVEQZVQPZWDVIVEQZ
      VMVLWDVFQZPZVIWDVFQWFVCWEVSVJTWHWIUAVJVSWEURVDBVEWDVLVIVGVHUSUTWKVQWGPWHV
      MVQWJWGWCVLWDVEWBFRZSUKVQWGULUMVIWDVEVKWLSVAVB $.
      $( [29-Jan-2015] $)

    $( Being a poset is a self-dual property.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    oduposb $p |- ( O e. V -> ( O e. Poset <-> D e. Poset ) ) $=
      ( va vb wcel cpo odupos codu cfv eqid cbs cvv a1i odubas cv cple wbr wa
      fvex id wceq eqidd oduleval eqcomi breqi vex brcnv 3bitri pospropd syl5ib
      wb ccnv impbid2 ) BCGZBHGZAHGZABDIURAJKZHGUPUQUSAUSLZIUPEFBMKZUSBNCUSNGUP
      AJUAOUPUBVAUSMKUCUPVAUSAUTVAABDVALPPOUPVAUDEQZFQZUSRKZSZVBVCBRKZSZUMUPVBV
      AGVCVAGTTVEVBVCVFUNZUNZSVCVBVHSVGVBVCVDVIVIVDUSVHAUTAVFBDVFLUEUEUFUGVBVCV
      HEUHZFUHZUIVCVBVFVKVJUIUJOUKULUO $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d a b $.

    $( Lemma for ~ odujoin . $)
    meet0 $p |- ( meet ` (/) ) = (/) $=
      ( va vb c0 cmee cfv cv cpr cglb cmpt2 cvv wcel wceq 0ex cbs cnx str0 eqid
      baseid meetfval ax-mp mpt20 eqtri ) CDEZABCCAFBFGCHEZEZIZCCJKUCUFLMABJCUD
      CUCNONEAARPUDQUCQSTCUEABUAUB $.
      $( [29-Jan-2015] $)

    $( Lemma for ~ odumeet . $)
    join0 $p |- ( join ` (/) ) = (/) $=
      ( va vb c0 cjn cfv cv cpr club cvv wcel wceq 0ex cbs cnx baseid str0 eqid
      cmpt2 joinfval ax-mp mpt20 eqtri ) CDEZABCCAFBFGCHEZEZRZCCIJUCUFKLABICUDU
      CCMNMEAAOPUDQUCQSTCUEABUAUB $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d D a b c d $.  $d L a b c d $.  $d U a b c d $.  $d O a b c d $.
    $d V a b c d $.  $d J a b $.  $d M a b $.
    oduglb.d $e |- D = ( ODual ` O ) $.

    ${
      oduglb.l $e |- U = ( lub ` O ) $.
      $( Greatest lower bounds in a dual order are least upper bounds in the
         original order.  (Contributed by Stefan O'Rear, 29-Jan-2015.) $)
      oduglb $p |- ( O e. V -> U = ( glb ` D ) ) $=
        ( va vc vb vd wcel cfv cv wbr wral vex brcnv ralbii eqid cvv cbs cpw wi
        club cglb cple wa crio cmpt wb imbi12i anbi12i riotabiia mpteq2i eqcomi
        ccnv a1i lubfval wceq codu fvex eqeltri odubas oduleval glbfval 3eqtr4a
        mp1i syl5eq ) CDKZBCUDLZAUELZFVIGCUALZUBZHMZIMZCUFLZNZHGMZOZVNJMZVPNZHV
        ROZVOVTVPNZUCZJVLOZUGZIVLUHZUIZGVMVOVNVPUPZNZHVROZVTVNWINZHVROZVTVOWINZ
        UCZJVLOZUGZIVLUHZUIZVJVKWSWHGVMWRWGWQWFIVLWQWFUJVOVLKWKVSWPWEWJVQHVRVOV
        NVPIPZHPZQRWOWDJVLWMWBWNWCWLWAHVRVTVNVPJPZXAQRVTVOVPXBWTQUKRULUQUMUNUOI
        HJDVLVJCVPGVLSZVPSZVJSURATKVKWSUSVIACUTLTECUTVAVBIHJTVLVKAWIGVLACEXCVCA
        VPCEXDVDVKSVEVGVFVH $.
        $( [29-Jan-2015] $)
    $}

    ${
      odumeet.j $e |- J = ( join ` O ) $.
      $( Meets in a dual order are joins in the original.  (Contributed by
         Stefan O'Rear, 29-Jan-2015.) $)
      odumeet $p |- J = ( meet ` D ) $=
        ( va vb cjn cfv cmee cvv wcel wceq cbs cv cmpt2 eqid codu c0 fvprc club
        cpr cglb oduglb fveq1d 3ad2ant1 mpt2eq3dva joinfval fvex eqeltri odubas
        meetfval mp1i 3eqtr4d syl5eq fveq2d meet0 syl6eq eqtr4d pm2.61i eqtri
        wn ) BCHIZAJIZECKLZVCVDMVEFGCNIZVFFOZGOZUBZCUAIZIZPFGVFVFVIAUCIZIZPZVCV
        DVEFGVFVFVKVMVEVGVFLVKVMMVHVFLVEVIVJVLAVJCKDVJQZUDUEUFUGFGKVFVJVCCVFQZV
        OVCQUHAKLVDVNMVEACRIZKDCRUIUJFGKVFVLAVDVFACDVPUKVLQVDQULUMUNVEVBZVCSVDC
        HTVRVDSJISVRASJVRAVQSDCRTUOUPUQURUSUTVA $.
        $( [29-Jan-2015] $)
    $}

    ${
      odulub.l $e |- L = ( glb ` O ) $.
      $( Least upper bounds in a dual order are greatest lower bounds in the
         original order.  (Contributed by Stefan O'Rear, 29-Jan-2015.) $)
      odulub $p |- ( O e. V -> L = ( lub ` D ) ) $=
        ( va vb vc vd wcel cfv cv wbr wral vex brcnv ralbii eqid cvv cbs cpw wi
        cglb club cple wa crio cmpt wb imbi12i anbi12i riotabiia mpteq2i eqcomi
        ccnv a1i glbfval wceq codu fvex eqeltri odubas oduleval lubfval 3eqtr4a
        mp1i syl5eq ) CDKZBCUDLZAUELZFVIGCUALZUBZHMZIMZCUFLZNZIGMZOZJMZVOVPNZIV
        ROZVTVNVPNZUCZJVLOZUGZHVLUHZUIZGVMVOVNVPUPZNZIVROZVOVTWINZIVROZVNVTWINZ
        UCZJVLOZUGZHVLUHZUIZVJVKWSWHGVMWRWGWQWFHVLWQWFUJVNVLKWKVSWPWEWJVQIVRVOV
        NVPIPZHPZQRWOWDJVLWMWBWNWCWLWAIVRVOVTVPWTJPZQRVNVTVPXAXBQUKRULUQUMUNUOH
        IJDVLVJCVPGVLSZVPSZVJSURATKVKWSUSVIACUTLTECUTVAVBHIJTVLVKAWIGVLACEXCVCA
        VPCEXDVDVKSVEVGVFVH $.
        $( [29-Jan-2015] $)
    $}

    ${
      odujoin.m $e |- M = ( meet ` O ) $.
      $( Joins in a dual order are meets in the original.  (Contributed by
         Stefan O'Rear, 29-Jan-2015.) $)
      odujoin $p |- M = ( join ` D ) $=
        ( va vb cmee cfv cjn cvv wcel wceq cbs cv cmpt2 eqid codu c0 fvprc cglb
        cpr club odulub fveq1d 3ad2ant1 mpt2eq3dva meetfval fvex eqeltri odubas
        joinfval mp1i 3eqtr4d syl5eq fveq2d join0 syl6eq eqtr4d pm2.61i eqtri
        wn ) BCHIZAJIZECKLZVCVDMVEFGCNIZVFFOZGOZUBZCUAIZIZPFGVFVFVIAUCIZIZPZVCV
        DVEFGVFVFVKVMVEVGVFLVKVMMVHVFLVEVIVJVLAVJCKDVJQZUDUEUFUGFGKVFVJCVCVFQZV
        OVCQUHAKLVDVNMVEACRIZKDCRUIUJFGKVFVLVDAVFACDVPUKVLQVDQULUMUNVEVBZVCSVDC
        HTVRVDSJISVRASJVRAVQSDCRTUOUPUQURUSUTVA $.
        $( [29-Jan-2015] $)
    $}

    $( Being a lattice is self-dual.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    odulatb $p |- ( O e. V -> ( O e. Lat <-> D e. Lat ) ) $=
      ( va vb wcel cpo cv cjn cfv co cbs cmee wa wral clat oduposb eqid islat
      wb ancom 2ralbii a1i anbi12d odubas odujoin odumeet 3bitr4g ) BCGZBHGZEIZ
      FIZBJKZLBMKZGZULUMBNKZLUOGZOZFUOPEUOPZOAHGZURUPOZFUOPEUOPZOBQGAQGUJUKVAUT
      VCABCDRUTVCUAUJUSVBEFUOUOUPURUBUCUDUEEFUOUNBUQUOSZUNSZUQSZTEFUOUQAUNUOABD
      VDUFAUQBDVFUGAUNBDVEUHTUI $.
      $( [29-Jan-2015] $)

    $( Being a complete lattice is self-dual.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    oduclatb $p |- ( O e. CLat <-> D e. CLat ) $=
      ( va ccla wcel cvv c0 club cfv wss cbs eqid codu eleq1d cpo cglb wa wal
      wi elex noel ssid cnx baseid str0 clatlubcl mpan2 mto fvprc syl5eq mtbiri
      wn con4i oduposb odulub fveq1d oduglb anbi12d syl5bb imbi2d albidv isclat
      cv ancom odubas 3bitr4g pm5.21nii ) BEFZBGFZAEFZBEUAVJVKVJUMZVKHEFZVMHHIJ
      ZJZHFZVOUBVMHHKVPHUCHHVNHLUDLJDDUEUFVNMUGUHUIVLAHEVLABNJHCBNUJUKOULUNVJBP
      FZDVDZBLJZKZVRBIJZJZVSFZVRBQJZJZVSFZRZTZDSZRAPFZVTVRAIJZJZVSFZVRAQJZJZVSF
      ZRZTZDSZRVIVKVJVQWJWIWSABGCUOVJWHWRDVJWGWQVTWGWFWCRVJWQWCWFVEVJWFWMWCWPVJ
      WEWLVSVJVRWDWKAWDBGCWDMZUPUQOVJWBWOVSVJVRWAWNAWABGCWAMZURUQOUSUTVAVBUSVSW
      AWDBDVSMZXAWTVCVSWKWNADVSABCXBVFWKMWNMVCVGVH $.
      $( [29-Jan-2015] $)

    $( Being a lattice is self-dual.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    odulat $p |- ( O e. Lat -> D e. Lat ) $=
      ( clat wcel odulatb ibi ) BDEADEABDCFG $.
      $( [29-Jan-2015] $)
  $}

