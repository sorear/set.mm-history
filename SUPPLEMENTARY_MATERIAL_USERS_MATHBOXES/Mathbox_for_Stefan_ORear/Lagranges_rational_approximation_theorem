$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Lagrange's rational approximation theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x a b c $.  $d A a b c d x y z w $.  $d B a b c d x y z w $.
    $( Lemma for ~ irrapx1 .  Divides the unit interval into ` B ` half-open
       sections and using the pigeonhole principle ~ fphpdo finds two multiples
       of ` A ` in the same section mod 1. $)
    irrapxlem1 $p |- ( ( A e. RR+ /\ B e. NN ) -> E. x e. ( 0 ... B ) E. y e. (
        0 ... B ) ( x < y /\ ( |_ ` ( B x. ( ( A x. x ) mod 1 ) ) ) = ( |_ ` (
        B x. ( ( A x. y ) mod 1 ) ) ) ) ) $=
      ( wcel cc0 co c1 cmul cmo cfl cr cz a1i cle wbr clt sylancl syl wb va crp
      cn wa cfz cmin cv cfv wss cuz fzssuz uzssz zssre sstri cvv ovex 0z adantl
      csdm nnz zsubcl cn0 simpr nnm1nn0 nn0ge0 3syl nnre ltm1 fzsdom2 syl311anc
      1z w3a 3imp ad2antlr rpre ad2antrr elfzelz zre remulcl syl2anc modcl flcl
      wn cc wceq recnd mul01 modge0 nngt0 lemul2 syl112anc mpbid eqbrtrrd lenlt
      1rp 0re fllt mtbid mpbird elnn0z sylanbrc caddc flle modlt ltmul2 breqtrd
      1re mulid1 lelttrd ax-1cn npcan breqtrrd zleltp1 elfz2nn0 ax-mp syl3anbrc
      nncn weq oveq2 oveq1d oveq2d fveq2d fphpdo ) CUBEZDUCEZUDZABUAFDUEGZFDHUF
      GZUEGZDCUAUGZIGZHJGZIGZKUHZDCAUGZIGZHJGZIGZKUHDCBUGZIGZHJGZIGZKUHYGLUIYFY
      GFUJUHZLFDUKUUCMLFULUMUNUNNYIUOEYFFYHUEUPNYFFMEZYHMEZDMEZFYHOPZYHDQPZYIYG
      USPZUUDYFUQNYFUUFHMEZUUEYEUUFYDDUTZURZVKDHVAZRUULYFYEYHVBEZUUGYDYEVCDVDZY
      HVEVFYFDLEZUUHYEUUPYDDVGZURDVHSUUDUUEUUFVLUUGUUHUUIFYHDVIVMVJYFYJYGEZUDZY
      NVBEZUUNYNYHOPZYNYIEZUUSYNMEZFYNOPZUUTUUSYMLEZUVCUUSUUPYLLEZUVEYEUUPYDUUR
      UUQVNZUUSYKLEZHUBEZUVFUUSCLEZYJLEZUVHYDUVJYEUURCVOVPUURUVKYFUURYJMEUVKYJF
      DVQYJVRSURCYJVSVTZWOYKHWARZDYLVSVTZYMWBSZUUSUVDYNFQPZWCZUUSYMFQPZUVPUUSFY
      MOPZUVRWCZUUSDFIGZFYMOUUSDWDEZUWAFWEUUSDUVGWFZDWGSUUSFYLOPZUWAYMOPZUUSUVH
      UVIUWDUVLWOYKHWHRUUSFLEZUVFUUPFDQPZUWDUWETUWFUUSWPNZUVMUVGYEUWGYDUURDWIVN
      ZFYLDWJWKWLWMUUSUWFUVEUVSUVTTUWHUVNFYMWNVTWLUUSUVEUUDUVRUVPTUVNUQYMFWQRWR
      UUSUWFYNLEZUVDUVQTUWHUUSUVCUWJUVOYNVRSZFYNWNVTWSYNWTXAYEUUNYDUURUUOVNUUSU
      VAYNYHHXBGZQPZUUSYNDUWLQUUSYNYMDUWKUVNUVGUUSUVEYNYMOPUVNYMXCSUUSYMDHIGZDQ
      UUSYLHQPZYMUWNQPZUUSUVHUVIUWOUVLWOYKHXDRUUSUVFHLEZUUPUWGUWOUWPTUVMUWQUUSX
      GNUVGUWIYLHDXEWKWLUUSUWBUWNDWEUWCDXHSXFXIYEUWLDWEZYDUURYEUWBHWDEUWRDXQXJD
      HXKRVNXLUUSUVCUUEUVAUWMTUVOUUSUUFUUJUUEYEUUFYDUURUUKVNVKUUMRYNYHXMVTWSYHU
      OEUVBUUTUUNUVAVLTDHUFUPUOYNYHXNXOXPUAAXRZYMYRKUWSYLYQDIUWSYKYPHJYJYOCIXSX
      TYAYBUABXRZYMUUBKUWTYLUUADIUWTYKYTHJYJYSCIXSXTYAYBYC $.
      $( [12-Sep-2014] $)

    $( Lemma for ~ irrapx1 .  Two multiples in the same bucket means they are
       very close mod 1. $)
    irrapxlem2 $p |- ( ( A e. RR+ /\ B e. NN ) -> E. x e. ( 0 ... B ) E. y e. (
        0 ... B ) ( x < y /\ ( abs ` ( ( ( A x. x ) mod 1 ) - ( ( A x. y ) mod
        1 ) ) ) < ( 1 / B ) ) ) $=
      ( wcel wa clt wbr cmul co c1 cfv wceq cc0 cmin cabs cr syl2anc cc recnd
      crp cn cv cmo cfl cfz wrex cdiv irrapxlem1 nnre adantl ad2antrr ad3antrrr
      caddc rpre elfzelz zre syl ad2antlr remulcl 1rp a1i modcl intfrac oveq12d
      fveq2d adantr simpr oveq1d flcl zcn 3syl pnpcan syl3anc cico 0re modelico
      cz 1re icodiamlt syl22anc ax-1cn subid1i syl6breq eqbrtrd ex resubcl recn
      wb abscl wne nngt0 gt0ne0 redivcl ltmul2 syl112anc cle nnnn0 nn0ge0 absid
      eqcomd absmul subdi 3eqtr2d divcan2 breq12d bitrd sylibrd anim2d reximdva
      cn0 mpd ) CUAEZDUBEZFZAUCZBUCZGHZDCXPIJZKUDJZIJZUELZDCXQIJZKUDJZIJZUELZMZ
      FZBNDUFJZUGZAYIUGXRXTYDOJZPLZKDUHJZGHZFZBYIUGZAYIUGABCDUIXOYJYPAYIXOXPYIE
      ZFZYHYOBYIYRXQYIEZFZYGYNXRYTYGYAYEOJZPLZKGHZYNYTYGUUCYTYGFZUUBYBYAKUDJZUN
      JZYFYEKUDJZUNJZOJZPLZKGYTUUBUUJMYGYTUUAUUIPYTYAUUFYEUUHOYTYAQEZYAUUFMYTDQ
      EZXTQEZUUKXOUULYQYSXNUULXMDUJUKULZYTXSQEZKUAEZUUMYTCQEZXPQEZUUOXMUUQXNYQY
      SCUOUMZYQUURXOYSYQXPVREUURXPNDUPXPUQURUSCXPUTRUUPYTVAVBZXSKVCRZDXTUTRZYAV
      DURYTYEQEZYEUUHMYTUULYDQEZUVCUUNYTYCQEZUUPUVDYTUUQXQQEZUVEUUSYSUVFYRYSXQV
      REUVFXQNDUPXQUQURUKCXQUTRUUTYCKVCRZDYDUTRZYEVDURVEVFVGUUDUUJYFUUEUNJZUUHO
      JZPLZKGUUDUUIUVJPUUDUUFUVIUUHOUUDYBYFUUEUNYTYGVHVIVIVFYTUVKKGHYGYTUVKUUEU
      UGOJZPLZKGYTUVJUVLPYTYFSEZUUESEUUGSEUVJUVLMYTUVCYFVREUVNUVHYEVJYFVKVLYTUU
      EYTUUKUUPUUEQEUVBUUTYAKVCRTYTUUGYTUVCUUPUUGQEUVHUUTYEKVCRTYFUUEUUGVMVNVFY
      TUVMKNOJZKGYTNQEZKQEZUUENKVOJZEZUUGUVREZUVMUVOGHUVPYTVPVBUVQYTVSVBZYTUUKU
      UPUVSUVBUUTYAKVQRYTUVCUUPUVTUVHUUTYEKVQRNKUUEUUGVTWAKWBWCWDWEVGWEWEWFYTYN
      DYLIJZDYMIJZGHZUUCYTYLQEZYMQEZUULNDGHZYNUWDWIYTYKQEZYKSEZUWEYTUUMUVDUWHUV
      AUVGXTYDWGRZYKWHYKWJVLYTUVQUULDNWKZUWFUWAUUNYTUULUWGUWKUUNXOUWGYQYSXNUWGX
      MDWLUKULZDWMRZKDWNVNUUNUWLYLYMDWOWPYTUWBUUBUWCKGYTUWBDPLZYLIJZDYKIJZPLZUU
      BYTDUWNYLIYTUWNDYTUULNDWQHZUWNDMUUNXOUWRYQYSXNUWRXMXNDXKEUWRDWRDWSURUKULD
      WTRXAVIYTDSEZUWIUWQUWOMYTDUUNTZYTYKUWJTDYKXBRYTUWPUUAPYTUWSXTSEYDSEUWPUUA
      MUWTYTXTUVATYTYDUVGTDXTYDXCVNVFXDYTKSEUWSUWKUWCKMYTKUWATUWTUWMKDXEVNXFXGX
      HXIXJXJXL $.
      $( [12-Sep-2014] $)

    $( Lemma for ~ irrapx1 .  By subtraction, there is a multiple very close to
       an integer. $)
    irrapxlem3 $p |- ( ( A e. RR+ /\ B e. NN ) -> E. x e. ( 1 ... B ) E. y e.
        NN0 ( abs ` ( ( A x. x ) - y ) ) < ( 1 / B ) ) $=
      ( wcel wa clt wbr co c1 cmin cabs cc0 cle wceq cr syl syl2anc cc recnd va
      vb crp cn cmul cmo cfv cdiv cfz wrex cn0 irrapxlem2 cfl ax-1cn subidi a1i
      cv wb elfzelz ad2antrl zre ad2antll posdif biimpa eqbrtrd simplrr simplrl
      cz 1z zsubcl zlem1lt mpbird 3syl resubcl 0re simpllr nnre elfzle1 syl3anc
      lesub2 mpbid subid1 elfzle2 letrd nnz elfz mpbir2and adantrr rpre remulcl
      ad3antrrr flcl simpr ltle syl21anc rpgt0 lemul2 syl112anc flwordi biimpar
      subge0 elnn0z sylanbrc oveq1d sub4 syl22anc modfrac eqcomd oveq12d 3eqtrd
      imp subdi fveq2d modcl abssub eqtr2d breq1d biimpd impr oveq2 rcla42ev ex
      1rp rexlimdvva mpd ) CUCEZDUDEZFZUAUQZUBUQZGHZCYIUEIZJUFIZCYJUEIZJUFIZKIL
      UGZJDUHIZGHZFZUBMDUIIZUJUAYTUJCAUQZUEIZBUQZKIZLUGZYQGHZBUKUJAJDUIIZUJZUAU
      BCDULYHYSUUHUAUBYTYTYHYIYTEZYJYTEZFZFZYSUUHUULYSFYJYIKIZUUGEZYNUMUGZYLUMU
      GZKIZUKEZCUUMUEIZUUQKIZLUGZYQGHZUUHUULYKUUNYRUULYKFZUUNJUUMNHZUUMDNHZUVCU
      VDJJKIZUUMGHZUVCUVFMUUMGUVFMOUVCJUNUOUPUULYKMUUMGHZUULYIPEZYJPEZYKUVHURUU
      LYIVHEZUVIUUIUVKYHUUJYIMDUSZUTYIVAZQUULYJVHEZUVJUUJUVNYHUUIYJMDUSZVBYJVAZ
      QYIYJVCRVDVEUVCJVHEZUUMVHEZUVDUVGURUVQUVCVIUPZUVCUVNUVKUVRUVCUUJUVNYHUUIU
      UJYKVFZUVOQUVCUUIUVKYHUUIUUJYKVGZUVLQYJYIVJRZJUUMVKRVLUVCUUMYJMKIZDUVCUVJ
      UVIUUMPEUVCUUJUVNUVJUVTUVOUVPVMZUVCUUIUVKUVIUWAUVLUVMVMZYJYIVNRUVCUVJMPEZ
      UWCPEUWDUWFUVCVOUPZYJMVNRUVCYGDPEYFYGUUKYKVPZDVQQUVCMYINHZUUMUWCNHZUVCUUI
      UWIUWAYIMDVRQUVCUWFUVIUVJUWIUWJURUWGUWEUWDMYIYJVTVSWAUVCUWCYJDNUVCYJSEZUW
      CYJOUVCYJUWDTZYJWBQUVCUUJYJDNHUVTYJMDWCQVEWDUVCUVRUVQDVHEZUUNUVDUVEFURUWB
      UVSUVCYGUWMUWHDWEQUUMJDWFVSWGWHUULYKUURYRUVCUUQVHEZMUUQNHZUURUVCUUOVHEZUU
      PVHEZUWNUVCYNPEZUWPUVCCPEZUVJUWRYFUWSYGUUKYKCWIWKZUWDCYJWJRZYNWLQZUVCYLPE
      ZUWQUVCUWSUVIUXCUWTUWECYIWJRZYLWLQZUUOUUPVJRUVCUUOPEZUUPPEZUUPUUONHZUWOUV
      CUWPUXFUXBUUOVAQZUVCUWQUXGUXEUUPVAQZUVCUXCUWRYLYNNHZUXHUXDUXAUVCYIYJNHZUX
      KUVCUVIUVJYKUXLUWEUWDUULYKWMUVIUVJFYKUXLYIYJWNXKWOUVCUVIUVJUWSMCGHZUXLUXK
      URUWEUWDUWTYFUXMYGUUKYKCWPWKYIYJCWQWRWAYLYNWSVSUXFUXGFUWOUXHUUOUUPXAWTWOU
      UQXBXCWHUULYKYRUVBUVCYRUVBUVCYPUVAYQGUVCUVAYOYMKIZLUGZYPUVCUUTUXNLUVCUUTY
      NYLKIZUUQKIZYNUUOKIZYLUUPKIZKIZUXNUVCUUSUXPUUQKUVCCSEUWKYISEUUSUXPOUVCCUW
      TTUWLUVCYIUWETCYJYIXLVSXDUVCYNSEYLSEUUOSEUUPSEUXQUXTOUVCYNUXATUVCYLUXDTUV
      CUUOUXITUVCUUPUXJTYNYLUUOUUPXEXFUVCUXRYOUXSYMKUVCYOUXRUVCUWRYOUXROUXAYNXG
      QXHUVCYMUXSUVCUXCYMUXSOUXDYLXGQXHXIXJXMUVCYOSEYMSEUXOYPOUVCYOUVCUWRJUCEZY
      OPEUXAUYAUVCYCUPZYNJXNRTUVCYMUVCUXCUYAYMPEUXDUYBYLJXNRTYOYMXORXPXQXRXSUUF
      UVBUUSUUCKIZLUGZYQGHABUUMUUQUUGUKUUAUUMOZUUEUYDYQGUYEUUDUYCLUYEUUBUUSUUCK
      UUAUUMCUEXTXDXMXQUUCUUQOZUYDUVAYQGUYFUYCUUTLUUCUUQUUSKXTXMXQYAVSYBYDYE $.
      $( [13-Sep-2014] $)

    $( Lemma for ~ irrapx1 .  Eliminate ranges, use positivity of the input to
       force positivity of the output by increasing ` B ` as needed. $)
    irrapxlem4 $p |- ( ( A e. RR+ /\ B e. NN ) -> E. x e. NN E. y e. NN ( abs `
        ( ( A x. x ) - y ) ) < ( 1 / if ( x <_ B , B , x ) ) ) $=
      ( va wcel cn wa co cmin c1 cdiv cle wbr clt cr cc0 syl2anc syl wb vb cmul
      crp cv cabs cfv cfl caddc cif cn0 wrex cfz simpl rpreccl rprege0 flge0nn0
      3syl nn0p1nn simpr ifcl irrapxlem3 simpllr elfznn nn0z ad2antlr ad3antrrr
      cz cneg cc rpre nnre remulcl nn0re resubcl recnd abscl wne rereccl 0reALT
      nnne0 a1i rpne0 flcl zre peano2re max2 nngt0 ltletrd lerec syl22anc mpbid
      max1 flltp1 ltle mpd wceq nncn recrec breqtrrd recgt0 rpgt0 mpbird mulid1
      wi letrd nnge1 1re lemul2 syl112anc eqbrtrrd subid1 simprd ltsub2 syl3anc
      abslt elnnz sylanbrc redivcl elfzle2 jca maxle weq oveq2 oveq1d fveq2d id
      breq1 ifbieq2d oveq2d breq12d breq1d rcla42ev ex rexlimdva ) CUCFZDGFZHZC
      EUDZUBIZUAUDZJIZUEUFZKDKCLIZUGUFZKUHIZMNZUUEDUIZLIZONZUAUJUKZEKUUGULIZUKZ
      CAUDZUBIZBUDZJIZUEUFZKUUMDMNZDUUMUIZLIZONZBGUKAGUKZYQYOUUGGFZUULYOYPUMZYQ
      UUEGFZYPUVCYQUUCPFZQUUCMNHZUUDUJFUVEYQYOUUCUCFUVGUVDCUNUUCUOUQUUCUPUUDURU
      QZYOYPUSZUUFUUEDGUTZREUACUUGVARYQUUJUVBEUUKYQYRUUKFZHZUUIUVBUAUJUVLYTUJFZ
      HZUUIUVBUVNUUIHZYRGFZYTGFZUUBKYRDMNZDYRUIZLIZONZUVBUVOUVKUVPYQUVKUVMUUIVB
      ZYRUUGVCSZUVOYTVGFZQYTONZUVQUVMUWDUVLUUIYTVDVEUVOUWEUUAYSQJIZONZUVOUWFVHU
      UAONZUWGUVOUUBUWFONZUWHUWGHZUVOUUBUUHUWFUVOUUAVIFUUBPFUVOUUAUVOYSPFZYTPFZ
      UUAPFZUVOCPFZYRPFZUWKUVOYOUWNYQYOUVKUVMUUIUVDVFZCVJSZUVOUVPUWOUWCYRVKSZCY
      RVLRZUVMUWLUVLUUIYTVMVEZYSYTVNRZVOUUAVPSZUVOUUGPFZUUGQVQZUUHPFUVOUVCUXCUV
      OUVEYPUVCYQUVEUVKUVMUUIUVHVFZYQYPUVKUVMUUIUVIVFZUVJRZUUGVKSZUVOUVCUXDUXGU
      UGVTSUUGVRRZUVOUWKQPFZUWFPFZUWSUXJUVOVSWAZYSQVNRZUVNUUIUSZUVOUUHCUWFUXIUW
      QUXMUVOUUHKUUELIZCUXIUVOUUEPFZUUEQVQZUXOPFZUVOUUDPFZUXPUVOUVFUUDVGFUXSUVO
      UWNCQVQZUVFUWQUVOYOUXTUWPCWBSCVRRZUUCWCUUDWDUQUUDWESZUVOUVEUXQUXEUUEVTSZU
      UEVRRZUWQUVOUUEUUGMNZUUHUXOMNZUVODPFZUXPUYEUVOYPUYGUXFDVKSZUYBDUUEWFRUVOU
      XPQUUEONZUXCQUUGONZUYEUYFTUYBUVOUVEUYIUXEUUEWGSZUXHUVOQDUUGUXLUYHUXHUVOYP
      QDONUXFDWGSZUVOUYGUXPDUUGMNZUYHUYBDUUEWLRZWHZUUEUUGWIWJWKUVOUXOCMNZUUCKUX
      OLIZMNZUVOUUCUUEUYQMUVOUUCUUEONZUUCUUEMNZUVOUVFUYSUYAUUCWMSUVOUVFUXPUYSUY
      TXDUYAUYBUUCUUEWNRWOUVOUUEVIFZUXQUYQUUEWPUVOUVEVUAUXEUUEWQSUYCUUEWRRWSUVO
      UXRQUXOONZUWNQCONZUYPUYRTUYDUVOUXPUYIVUBUYBUYKUUEWTRUWQUVOYOVUCUWPCXASZUX
      OCWIWJXBXEUVOCYSUWFMUVOCKUBIZCYSMUVOCVIFVUECWPUVOCUWQVOCXCSUVOKYRMNZVUEYS
      MNZUVOUVPVUFUWCYRXFSUVOKPFZUWOUWNVUCVUFVUGTVUHUVOXGWAZUWRUWQVUDKYRCXHXIWK
      XJUVOYSVIFUWFYSWPUVOYSUWSVOYSXKSWSXEWHUVOUWMUXKUWIUWJTUXAUXMUUAUWFXORWKXL
      UVOUXJUWLUWKUWEUWGTUXLUWTUWSQYTYSXMXNXBYTXPXQUVOUUBUUHUVTUXBUXIUVOVUHUVSP
      FZUVSQVQZUVTPFVUIUVOUVSGFZVUJUVOYPUVPVULUXFUWCUVRDYRGUTRZUVSVKSUVOVULVUKV
      UMUVSVTSKUVSXRXNUXNUVOUVSUUGMNZUUHUVTMNZUVOVUNYRUUGMNZUYMHZUVOVUPUYMUVOUV
      KVUPUWBYRKUUGXSSUYNXTUVOUWOUYGUXCVUNVUQTUWRUYHUXHYRDUUGYAXNXBUVOVUJQUVSON
      UXCUYJVUNVUOTUVOUYGUWOVUJUYHUWRUVRDYRPUTRZUVOQDUVSUXLUYHVURUYLUVOUWOUYGDU
      VSMNUWRUYHYRDWFRWHUXHUYOUVSUUGWIWJWKWHUVAUWAYSUUOJIZUEUFZUVTONABYRYTGGAEY
      BZUUQVUTUUTUVTOVVAUUPVUSUEVVAUUNYSUUOJUUMYRCUBYCYDYEVVAUUSUVSKLVVAUURUVRU
      UMYRDUUMYRDMYGVVAYFYHYIYJBUAYBZVUTUUBUVTOVVBVUSUUAUEUUOYTYSJYCYEYKYLXNYMY
      NYNWO $.
      $( [13-Sep-2014] $)

    $( Lemma for ~ irrapx1 .  Switching to real intervals and fraction
       syntax. $)
    irrapxlem5 $p |- ( ( A e. RR+ /\ B e. RR+ ) -> E. x e. QQ ( 0 < x /\ ( abs
        ` ( x - A ) ) < B /\ ( abs ` ( x - A ) ) < ( ( denom ` x ) ^ -u 2 ) ) )
        $=
      ( crp wcel cmul co cabs cfv c1 cdiv cle wbr clt cc0 cr syl syl2anc wceq
      cc va vb wa cv cmin cfl caddc cif cn wrex cdenom c2 cneg w3a cq cn0 simpr
      cexp rpreccl rpre jca flge0nn0 nn0p1nn 3syl irrapxlem4 syldan wne simplrr
      rpge0 nnq simplrl nnne0 qdivcl syl3anc nnrp rpgt0 nnre nnnn0 nn0ge0 absid
      rpdivcl eqcomd oveq1d recnd qre ad3antrrr resubcl absmul qcn rpcn divcan2
      subdi mulcom oveq12d eqtrd fveq2d remulcl 3eqtr2d recn abscl simpllr ifcl
      abssub gt0ne0 rereccl flltp1 ltle imp syl21anc letrd lerec syl22anc mpbid
      max2 wb rpne0 recrec mulid2 eqtr4d nnge1 1re a1i lemul1 syl112anc eqbrtrd
      ltletrd nngt0 ltmul2 mpbird msqgt0 qdencl max1 divdiv1 syl122anc 3eqtr3rd
      divid divrec 3brtr4d cz nnz divdenle le2msq nncn 2nn0 expneg sqval oveq2d
      breqtrrd breq2 oveq1 breq1d breq12d 3anbi123d rcla4ev syl13anc rexlimdvva
      fveq2 ex mpd ) BDEZCDEZUCZBUAUDZFGZUBUDZUEGZHIZJUVCJCKGZUFIZJUGGZLMZUVJUV
      CUHZKGZNMZUBUIUJUAUIUJZOAUDZNMZUVPBUEGZHIZCNMZUVSUVPUKIZULUMZURGZNMZUNZAU
      OUJZUUTUVAUVJUIEZUVOUVBUVHPEZOUVHLMZUCUVIUPEZUWGUVBUWHUWIUVBUVHDEZUWHUVBU
      VAUWKUUTUVAUQCUSZQZUVHUTZQUVBUWKUWIUWMUVHVIZQVAUVHVBZUVIVCZVDUAUBBUVJVEVF
      UVBUVNUWFUAUBUIUIUVBUVCUIEZUVEUIEZUCZUCZUVNUWFUXAUVNUCZUVEUVCKGZUOEZOUXCN
      MZUXCBUEGZHIZCNMZUXGUXCUKIZUWBURGZNMZUWFUXBUVEUOEZUVCUOEZUVCOVGZUXDUXBUWS
      UXLUVBUWRUWSUVNVHZUVEVJQUXBUWRUXMUVBUWRUWSUVNVKZUVCVJQUXBUWRUXNUXPUVCVLQZ
      UVEUVCVMVNZUXBUXCDEZUXEUXBUVEDEZUVCDEZUXSUXBUWSUXTUXOUVEVOQUXBUWRUYAUXPUV
      CVOQZUVEUVCWARUXCVPQUXBUXHUVCUXGFGZUVCCFGZNMZUXBUYCUVGUYDNUXBUYCUVCHIZUXG
      FGZUVCUXFFGZHIZUVGUXBUVCUYFUXGFUXBUYFUVCUXBUVCPEZOUVCLMZUYFUVCSUXBUWRUYJU
      XPUVCVQQZUXBUWRUVCUPEUYKUXPUVCVRUVCVSVDZUVCVTRWBWCUXBUVCTEZUXFTEZUYIUYGSU
      XBUVCUYLWDZUXBUXFUXBUXCPEZBPEZUXFPEZUXBUXDUYQUXRUXCWEQUUTUYRUVAUWTUVNBUTW
      FZUXCBWGRZWDUVCUXFWHRUXBUYIUVEUVDUEGZHIZUVGUXBUYHVUBHUXBUYHUVCUXCFGZUVCBF
      GZUEGZVUBUXBUYNUXCTEZBTEZUYHVUFSUYPUXBUXDVUGUXRUXCWIQUUTVUHUVAUWTUVNBWJWF
      ZUVCUXCBWLVNUXBVUDUVEVUEUVDUEUXBUVETEZUYNUXNVUDUVESUXBUVEUXBUWSUVEPEZUXOU
      VEVQQZWDZUYPUXQUVEUVCWKVNUXBUYNVUHVUEUVDSUYPVUIUVCBWMRWNWOWPUXBVUJUVDTEVU
      CUVGSVUMUXBUVDUXBUYRUYJUVDPEZUYTUYLBUVCWQRZWDUVEUVDXCRWOWRZUXBUVGUVMUYDUX
      BUVFPEZUVFTEUVGPEUXBVUNVUKVUQVUOVULUVDUVEWGRUVFWSUVFWTVDZUXBUVLPEZUVLOVGZ
      UVMPEUXBUVJPEZUYJVUSUXBUWGVVAUXBUWJUWGUXBUWHUWIUWJUXBUWKUWHUXBUVAUWKUUTUV
      AUWTUVNXAZUWLQZUWNQZUXBUWKUWIVVCUWOQUWPRUWQQZUVJVQQZUYLUVKUVJUVCPXBRZUXBV
      USOUVLNMZVUTVVGUXBUVLDEZVVHUXBUVJDEZUYAVVIUXBUWGVVJVVEUVJVOQUYBUVKUVJUVCD
      XBRUVLVPQZUVLXDRUVLXERZUXBUYJCPEZUYDPEUYLUXBUVAVVMVVBCUTQZUVCCWQRZUXAUVNU
      QZUXBUVMJUVHKGZUYDVVLUXBUWKVVQDEVVQPEVVCUVHUSVVQUTVDVVOUXBUVHUVLLMZUVMVVQ
      LMZUXBUVHUVJUVLVVDVVFVVGUXBUWHVVAUVHUVJNMZUVHUVJLMZVVDVVFUXBUWHVVTVVDUVHX
      FQUWHVVAUCVVTVWAUVHUVJXGXHXIUXBUYJVVAUVJUVLLMUYLVVFUVCUVJXNRXJUXBUWHOUVHN
      MZVUSVVHVVRVVSXOVVDUXBUWKVWBVVCUVHVPQVVGVVKUVHUVLXKXLXMUXBVVQJCFGZUYDLUXB
      VVQCVWCUXBCTEZCOVGZVVQCSUXBCVVNWDZUXBUVAVWEVVBCXPQCXQRUXBVWDVWCCSVWFCXRQX
      SUXBJUVCLMZVWCUYDLMZUXBUWRVWGUXPUVCXTQUXBJPEZUYJVVMOCNMZVWGVWHXOVWIUXBYAY
      BUYLVVNUXBUVAVWJVVBCVPQJUVCCYCYDXMYEXJYFYEUXBUXGPEZVVMUYJOUVCNMZUXHUYEXOU
      XBUYSUYOVWKVUAUXFWSUXFWTVDZVVNUYLUXBUWRVWLUXPUVCYGQZUXGCUVCYHYDYIUXBUXGJU
      XIUXIFGZKGZUXJNUXBUXGJUVCUVCFGZKGZVWPVWMUXBVWQPEZVWQOVGZVWRPEZUXBUYJUYJVW
      SUYLUYLUVCUVCWQRZUXBVWSOVWQNMZVWTVXBUXBUYJUXNVXCUYLUXQUVCYJRZVWQXDRZVWQXE
      RZUXBVWOPEZVWOOVGZVWPPEUXBUXIPEZVXIVXGUXBUXIUIEZVXIUXBUXDVXJUXRUXCYKQZUXI
      VQQZVXLUXIUXIWQRZUXBVXGOVWONMZVXHVXMUXBVXIUXIOVGZVXNVXLUXBVXJVXOVXKUXIVLQ
      UXIYJRZVWOXDRVWOXERUXBUXGVWRNMZUYCUVCVWRFGZNMZUXBUVGJUVCKGZUYCVXRNUXBUVGU
      VMVXTVURVVLUXBUYJUXNVXTPEUYLUXQUVCXERVVPUXBUVCUVLLMZUVMVXTLMZUXBUYJVVAVYA
      UYLVVFUVCUVJYLRUXBUYJVWLVUSVVHVYAVYBXOUYLVWNVVGVVKUVCUVLXKXLXMYFVUPUXBUVC
      UVCKGZUVCKGZUVCVWQKGZVXTVXRUXBUYNUYNUXNUYNUXNVYDVYESUYPUYPUXQUYPUXQUVCUVC
      UVCYMYNUXBVYCJUVCKUXBUYNUXNVYCJSUYPUXQUVCYPRWCUXBUYNVWQTEVWTVYEVXRSUYPUXB
      VWQVXBWDVXEUVCVWQYQVNYOYRUXBVWKVXAUYJVWLVXQVXSXOVWMVXFUYLVWNUXGVWRUVCYHYD
      YIUXBVWOVWQLMZVWRVWPLMZUXBUXIUVCLMZVYFUXBUVEYSEZUWRVYHUXBUWSVYIUXOUVEYTQU
      XPUVEUVCUUARUXBVXIOUXILMZUYJUYKVYHVYFXOVXLUXBVXJUXIUPEVYJVXKUXIVRUXIVSVDU
      YLUYMUXIUVCUUBXLXMUXBVXGVXNVWSVXCVYFVYGXOVXMVXPVXBVXDVWOVWQXKXLXMYFUXBUXJ
      JUXIULURGZKGZVWPUXBUXITEZULUPEZUXJVYLSUXBVXJVYMVXKUXIUUCQZVYNUXBUUDYBUXIU
      LUUERUXBVYKVWOJKUXBVYMVYKVWOSVYOUXIUUFQUUGWOUUHUWEUXEUXHUXKUNAUXCUOUVPUXC
      SZUVQUXEUVTUXHUWDUXKUVPUXCONUUIVYPUVSUXGCNVYPUVRUXFHUVPUXCBUEUUJWPZUUKVYP
      UVSUXGUWCUXJNVYQVYPUWAUXIUWBURUVPUXCUKUUQWCUULUUMUUNUUOUURUUPUUS $.
      $( [13-Sep-2014] $)

    $( Lemma for ~ irrapx1 .  Explicit description of a non-closed set. $)
    irrapxlem6 $p |- ( ( A e. RR+ /\ B e. RR+ ) -> E. x e. { y e. QQ | ( 0 < y
        /\ ( abs ` ( y - A ) ) < ( ( denom ` y ) ^ -u 2 ) ) } ( abs ` ( x - A )
        ) < B ) $=
      ( va crp wcel wa cc0 cv clt wbr cmin co cabs cfv cdenom cexp cq wrex cneg
      w3a crab irrapxlem5 simplr simpr1 simpr3 jca weq breq2 oveq1 fveq2d fveq2
      c2 oveq1d breq12d anbi12d elrab sylanbrc simpr2 breq1d rcla4ev syl2anc ex
      rexlimdva mpd ) CFGDFGHZIEJZKLZVHCMNZOPZDKLZVKVHQPZUNUAZRNZKLZUBZESTAJZCM
      NZOPZDKLZAIBJZKLZWBCMNZOPZWBQPZVNRNZKLZHZBSUCZTZECDUDVGVQWKESVGVHSGZHZVQW
      KWMVQHZVHWJGZVLWKWNWLVIVPHZWOVGWLVQUEWNVIVPWMVIVLVPUFWMVIVLVPUGUHWIWPBVHS
      BEUIZWCVIWHVPWBVHIKUJWQWEVKWGVOKWQWDVJOWBVHCMUKULWQWFVMVNRWBVHQUMUOUPUQUR
      USWMVIVLVPUTWAVLAVHWJAEUIZVTVKDKWRVSVJOVRVHCMUKULVAVBVCVDVEVF $.
      $( [13-Sep-2014] $)

    $( Dirichlet's approximation theorem.  Every positive irrational number has
       infinitely many rational approximations which are closer than the
       inverse squares of their reduced denominators.  Lemma 61 in
       [vandenDries] p. 42.  (Contributed by Stefan O'Rear, 14-Sep-2014.) $)
    irrapx1 $p |- ( A e. ( RR+ \ QQ ) -> { y e. QQ | ( 0 < y /\ ( abs ` ( y - A
        ) ) < ( ( denom ` y ) ^ -u 2 ) ) } ~~ NN ) $=
      ( vb va crp cq wcel com cen wbr cn wa cv clt cmin co cabs cfv wss cr cdif
      cc0 cdenom c2 cneg cexp crab cfn wn qnnen nnenom entri pm3.2i wrex ssrab2
      wral qssre sstri eldifi rpre eldifn sseli nsyl irrapxlem6 sylan ralrimiva
      a1i syl rencldnfi syl31anc jctil ctbnfien sylancr ) BEFUAGZFHIJZKHIJZLUBA
      MZNJVQBOPQRVQUCRUDUEUFPNJLZAFUGZFSZVSUHGUIZLVSKIJVOVPFKHUJUKULUKUMVNWAVTV
      NVSTSZBTGZBVSGZUICMBOPQRDMZNJCVSUNZDEUPWAWBVNVSFTVRAFUOZUQURVGVNBEGZWCBEF
      USZBUTVHVNBFGWDBEFVAVSFBWGVBVCVNWFDEVNWHWEEGWFWICABWEVDVEVFDCVSBVIVJWGVKV
      SFKVLVM $.
      $( [14-Sep-2014] $)
  $}

