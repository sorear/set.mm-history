$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Definition of the structure product
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Structure definition utility lemma.  To prove that an explicit function is
     a function using O(n) steps, exploit the order properties of the index
     set.  Zero-pair case. $)
  strlemor0 $p |- ( Fun (/) /\ dom (/) C_ ( 1 ... 0 ) ) $=
    ( c0 wfun cdm c1 cc0 cfz co wss fun0 dm0 0ss eqsstri pm3.2i ) ABACZDEFGZHIN
    AOJOKLM $.
    $( [3-Jan-2015] $)

  ${
    strlemor.o $e |- I < J $.
    strlemor.i $e |- I e. NN0 $.
    strlemor.j $e |- J e. NN $.
    strlemor.f $e |- ( Fun F /\ dom F C_ ( 1 ... I ) ) $.

    ${
      strlemor1.g $e |- G = ( F u. { <. J , X >. } ) $.
      $( Add one element to the end of a structure. $)
      strlemor1 $p |- ( Fun G /\ dom G C_ ( 1 ... J ) ) $=
        ( wfun cdm c1 cfz co wss csn c0 wcel wbr cop cun wa cin simpli cn elexi
        wceq funsn pm3.2i dmsnop ineq2i wn cle nn0rei nnrei ltnlei mpbi elfzle2
        clt mto simpri sseli disjsn mpbir eqtri funun mp2an funeqi dmun cuz cfv
        dmeqi nn0zi nnzi ltleii eluz2 mpbir3an fzss2 ax-mp sstri elfz1end snssi
        cz eqsstri unssi ) BKZBLZMDNOZPWGADEUAQZUBZKZAKZWJKZUCALZWJLZUDZRUHWLWM
        WNWMWOMCNOZPZIUEDEDUFHUGUIUJWQWODQZUDZRWPWTWODEUKZULXARUHDWOSZUMXCDWRSZ
        XDDCUNTZCDUTTXEUMFCDCGUOZDHUPZUQURDMCUSVAWOWRDWMWSIVBZVCVAWODVDVEVFAWJV
        GVHBWKJVIVEWHWOWPUBZWIWHWKLXIBWKJVMAWJVJVFWOWPWIWOWRWIXHDCVKVLSZWRWIPXJ
        CWDSDWDSCDUNTCGVNDHVOCDXFXGFVPCDVQVRCMDVSVTWAWPWTWIXBDWISZWTWIPDUFSXKHD
        WBURDWIWCVTWEWFWEUJ $.
        $( [3-Jan-2015] $)
    $}

    strlemor2.o $e |- J < K $.
    strlemor2.k $e |- K e. NN $.

    ${
      strlemor2.g $e |- G = ( F u. { <. J , X >. , <. K , Y >. } ) $.
      $( Add one element to the end of a structure. $)
      strlemor2 $p |- ( Fun G /\ dom G C_ ( 1 ... K ) ) $=
        ( cop csn cun nnnn0i eqid strlemor1 cpr df-pr uneq2i unass 3eqtr4i ) AD
        FOZPZQZBDEGLDJRMAUHCDFHIJKUHSTAUFEGOZUAZQAUGUIPZQZQBUHUKQUJULAUFUIUBUCN
        AUGUKUDUET $.
        $( [3-Jan-2015] $)
    $}

    strlemor3.o $e |- K < L $.
    strlemor3.l $e |- L e. NN $.

    ${
      strlemor3.g $e |- G = ( F u. { <. J , X >. , <. K , Y >. ,
          <. L , Z >. } ) $.
      $( Add three elements to the end of a structure. $)
      strlemor3 $p |- ( Fun G /\ dom G C_ ( 1 ... L ) ) $=
        ( cop cun cpr nnnn0i strlemor2 ctp df-tp uneq2i unass 3eqtr4i strlemor1
        eqid csn ) ADGSZEHSZUAZTZBEFIPEOUBQAUOCDEGHJKLMNOUOUJUCAULUMFISZUDZTAUN
        UPUKZTZTBUOURTUQUSAULUMUPUEUFRAUNURUGUHUI $.
        $( [3-Jan-2015] $)
    $}
  $}

  ${
    $d S s r x v f g $.  $d R s r x v f g $.  $d W s r x v f g $.
    $d Z s r x v f g $.  $d B s r v f g $.  $d A s r v $.  $d T s r v $.
    $d M s r v $.  $d O s r x v f g $.  $d L s r v $.  $d D s r v $.
    $d U s r v $.
    $( The structure product is a well-behaved binary operator. $)
    reldmprdm $p |- Rel dom Xm_ $=
      ( vs vr vv vx vf vg cvv cv cfv cbs cnx cop cplusg co cmpt cmpt2 cmulr ctp
      cvsca cpr cdm cixp csca cun cts ccom cpt cple wss wbr wral wa cds crn cxr
      copab clt csup csb cprdm df-prdm reldmmpt2 ) ABGGCDBHZUAZDHZVCIZJIUBKJICH
      ZLKMIEFVGVGDVDVEEHZIZVEFHZIZVFMINOPLKQIEFVGVGDVDVIVKVFQINOPLRKUCIAHZLKSIE
      FVLJIVGDVDVHVKVFSINOPLTUDKUEIUEVCUFUGILKUHIVHVJTVGUIVIVKVFUHIUJDVDUKULEFU
      PLKUMIEFVGVGDVDVIVKVFUMINOUNUOUQURPLRUDUSUTDCEFABVAVB $.
      $( [7-Jan-2015] $)

    prdmval.b $e |- B = X_ x e. dom R ( Base ` ( R ` x ) ) $.
    $( Lemma for structure products. $)
    prdmbaseex $p |- ( R e. W -> B e. _V ) $=
      ( wcel cdm cv cfv cbs cixp cvv wral dmexg fvex rgenw a1i syl2anc syl5eqel
      ixpexg ) CDFZBACGZAHCIZJIZKZLEUAUBLFUDLFZAUBMZUELFCDNUGUAUFAUBUCJOPQAUBUD
      LLTRS $.
      $( [3-Jan-2015] $)

    prdmval.a $e |- A = ( f e. B , g e. B |-> ( x e. dom R |->
        ( ( f ` x ) ( +g ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.t $e |- T = ( f e. B , g e. B |-> ( x e. dom R |->
        ( ( f ` x ) ( .r ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.m $e |- M = ( f e. ( Base ` S ) , g e. B |-> ( x e. dom R |->
        ( f ( vsca ` ( R ` x ) ) ( g ` x ) ) ) ) $.
    prdmval.o $e |- O = ( tP ` ( TopOpen o. R ) ) $.
    prdmval.l $e |- L = { <. f , g >. | ( { f , g } C_ B /\
        A. x e. dom R ( f ` x ) ( le ` ( R ` x ) ) ( g ` x ) ) } $.
    prdmval.d $e |- D = ( f e. B , g e. B |-> sup ( ran ( x e. dom R |->
        ( ( f ` x ) ( dist ` ( R ` x ) ) ( g ` x ) ) ) , RR* , < ) ) $.
    prdmval.u $e |- U = ( ( { <. ( Base ` ndx ) , B >. ,
        <. ( +g ` ndx ) , A >. , <. ( .r ` ndx ) , T >. } u.
        { <. ( Scalar ` ndx ) , S >. , <. ( vsca ` ndx ) , M >. } ) u.
        { <. ( TopOpen ` ndx ) , O >. , <. ( le ` ndx ) , L >. ,
        <. ( dist ` ndx ) , D >. } ) $.
    $( Value of the structure product. $)
    prdmval $p |- ( ( S e. W /\ R e. Z ) -> ( S Xm_ R ) = U ) $=
      ( vs vr vv wcel cvv cprdm co wceq elex cv cdm cfv cbs cixp cnx cop cplusg
      cmpt cmpt2 cmulr ctp csca cvsca cpr cun cts ccom cpt cple wss wbr wral wa
      copab cds crn cxr clt csup csb ixpeq1 fveq1 fveq2d ralrimivw ixpeq2 eqtrd
      dmeq syl syl6eqr adantl csbeq1d vex eleq1 mpbii prdmbaseex opeq2 mpt2eq12
      anidms opeq2d tpeq123d eqid mpan preq2d uneq12d eqidd sseq2 anbi1d csbied
      opabbidv oveqd 3ad2ant1 mpt2eq3dva adantr fveq2 mpt2eq123dv preq12d coeq2
      mpteq12dv breqd raleqbidv rneqd supeq1d df-prdm tpex prex eqeltri ovmpt2a
      anbi2d unex syl2an ) FNUGFUHUGEUHUGZFEUIUJHUKEOUGFNULEOULUDUEFEUHUHUFAUEU
      MZUNZAUMZYOUOZUPUOZUQZURUPUOZUFUMZUSZURUTUOZIJUUBUUBAYPYQIUMZUOZYQJUMZUOZ
      YRUTUOZUJZVAZVBZUSZURVCUOZIJUUBUUBAYPUUFUUHYRVCUOZUJZVAZVBZUSZVDZURVEUOZU
      DUMZUSZURVFUOZIJUVBUPUOZUUBAYPUUEUUHYRVFUOZUJZVAZVBZUSZVGZVHZURVIUOZVIYOV
      JZVKUOZUSZURVLUOZUUEUUGVGZUUBVMZUUFUUHYRVLUOZVNZAYPVOZVPZIJVQZUSZURVRUOZI
      JUUBUUBAYPUUFUUHYRVRUOZUJZVAZVSZVTWAWBZVBZUSZVDZVHZWCZHUIUVBFUKZYOEUKZVPZ
      UWPUFCUWOWCZHUWSUFYTCUWOUWRYTCUKUWQUWRYTAEUNZYQEUOZUPUOZUQZCUWRYTAUXAYSUQ
      ZUXDUWRYPUXAUKYTUXEUKYOEWJZAYPUXAYSWDWKUWRYSUXCUKZAUXAVOUXEUXDUKUWRUXGAUX
      AUWRYRUXBUPYQYOEWEZWFWGAUXAYSUXCWHWKWIPWLWMWNUWSUWTUUACUSZUUDIJCCUUKVBZUS
      ZUUNIJCCUUQVBZUSZVDZUVCUVDIJUVECUVHVBZUSZVGZVHZUVPUVQUVRCVMZUWBVPZIJVQZUS
      ZUWFIJCCUWKVBZUSZVDZVHZHUWSUFCUWOUYFUHUWSYNCUHUGUWRYNUWQUWRYOUHUGYNUEWOYO
      EUHWPWQWMACEUHPWRWKUUBCUKZUWOUYFUKUWSUYGUVLUXRUWNUYEUYGUUTUXNUVKUXQUYGUUC
      UXIUUMUXKUUSUXMUUBCUUAWSUYGUULUXJUUDUYGUULUXJUKIJUUBUUBCCUUKWTXAXBUYGUURU
      XLUUNUYGUURUXLUKIJUUBUUBCCUUQWTXAXBXCUYGUVJUXPUVCUYGUVIUXOUVDUVEUVEUKUYGU
      VIUXOUKUVEXDIJUVEUUBUVECUVHWTXEXBXFXGUYGUVPUVPUWEUYBUWMUYDUYGUVPXHUYGUWDU
      YAUVQUYGUWCUXTIJUYGUVSUXSUWBUUBCUVRXIXJXLXBUYGUWLUYCUWFUYGUWLUYCUKIJUUBUU
      BCCUWKWTXAXBXCXGWMXKUWSUYFUXIUUDBUSZUUNGUSZVDZUVAFUSZUVDLUSZVGZVHZUVMMUSZ
      UVQKUSZUWFDUSZVDZVHZHUWSUXRUYNUYEUYRUWSUXNUYJUXQUYMUWSUXIUXIUXKUYHUXMUYIU
      WSUXIXHUWSUXJBUUDUWRUXJBUKUWQUWRUXJIJCCAUXAUUFUUHUXBUTUOZUJZVAZVBBUWRIJCC
      UUKVUBUWRUUECUGZUUKVUBUKUUGCUGZUWRAYPUUJUXAVUAUXFUWRUUIUYTUUFUUHUWRYRUXBU
      TUXHWFXMYAXNXOQWLWMXBUWSUXLGUUNUWRUXLGUKUWQUWRUXLIJCCAUXAUUFUUHUXBVCUOZUJ
      ZVAZVBGUWRIJCCUUQVUGUWRVUCUUQVUGUKVUDUWRAYPUUPUXAVUFUXFUWRUUOVUEUUFUUHUWR
      YRUXBVCUXHWFXMYAXNXORWLWMXBXCUWSUVCUYKUXPUYLUWQUVCUYKUKUWRUVBFUVAWSXPUWSU
      XOLUVDUWSUXOIJFUPUOZCAUXAUUEUUHUXBVFUOZUJZVAZVBLUWSIJUVECUVHVUHCVUKUWQUVE
      VUHUKUWRUVBFUPXQXPUWSCXHUWRUVHVUKUKUWQUWRAYPUVGUXAVUJUXFUWRUVFVUIUUEUUHUW
      RYRUXBVFUXHWFXMYAWMXRSWLXBXSXGUWSUVPUYOUYBUYPUYDUYQUWSUVOMUVMUWRUVOMUKUWQ
      UWRUVOVIEVJZVKUOMUWRUVNVULVKYOEVIXTWFTWLWMXBUWRUYBUYPUKUWQUWRUYAKUVQUWRUY
      AUXSUUFUUHUXBVLUOZVNZAUXAVOZVPZIJVQKUWRUXTVUPIJUWRUWBVUOUXSUWRUWAVUNAYPUX
      AUXFUWRUVTVUMUUFUUHUWRYRUXBVLUXHWFYBYCYKXLUAWLXBWMUWSUYCDUWFUWSUYCIJCCAUX
      AUUFUUHUXBVRUOZUJZVAZVSZVTWAWBZVBDUWSIJCCUWKVVAUWSVUCUWKVVAUKZVUDUWRVVBUW
      QUWRVTUWJVUTWAUWRUWIVUSUWRAYPUWHUXAVURUXFUWRUWGVUQUUFUUHUWRYRUXBVRUXHWFXM
      YAYDYEWMXNXOUBWLXBXCXGUCWLWIWIAUFIJUDUEYFHUYSUHUCUYNUYRUYJUYMUXIUYHUYIYGU
      YKUYLYHYLUYOUYPUYQYGYLYIYJYM $.
      $( [3-Jan-2015] $)

    $( Structure product value exists. $)
    prdmvalex $p |- U e. _V $=
      ( cnx cbs cfv cop cmulr ctp csca cvsca cpr cun cts cple cds cvv tpex prex
      cplusg unex eqeltri ) HUBUCUDCUEZUBURUDBUEZUBUFUDGUEZUGZUBUHUDFUEZUBUIUDL
      UEZUJZUKZUBULUDMUEZUBUMUDKUEZUBUNUDDUEZUGZUKUOUAVHVLVDVGVAVBVCUPVEVFUQUSV
      IVJVKUPUSUT $.
      $( [3-Jan-2015] $)

    $( Structure product value is a function. $)
    prdmvalfun $p |- Fun U $=
      ( wfun cdm c1 c10 c2 caddc cfz wss cnx cbs cfv cop cplusg cmulr ctp cvsca
      co csca cpr cun c6 c9 c8 clt wbr 6lt8 8lt9 6re 8re 9re lttri mp2an nnnn0i
      6nn 9nn c3 c5 3lt5 3nn0 5nn cc0 lt01 0nn0 1nn strlemor0 1lt2 2nn 2lt3 3nn
      c0 un0 wceq wtru basendx opeq1i a1i df-plusg ndxarg df-mulr tpeq123d trud
      uncom 3eqtr4ri strlemor3 5lt6 df-sca df-vsca uneq2i strlemor2 ltp1i df-10
      preq12i breqtrri 10nn cr wcel crp 10re 2re 2pos elrpii ltaddrp cn nnaddcl
      cts cple cds df-topset df-ple df-ds eqtri simpli ) HUBHUCUDUEUFUGURZUHURU
      IUJUKULZCUMZUJUNULZBUMZUJUOULZGUMZUPZUJUSULZFUMZUJUQULZLUMZUTZVAZHVBVCUEY
      NMKDVBVDVEVFVDVCVEVFVBVCVEVFVGVHVBVDVCVIVJVKVLVMVBVOVNVPUUAUUGVQVRVBFLVSV
      TWAWKUUAWBUDUFVQCBGWCWDWEWFWGWHWIWJUDCUMZUFBUMZVQGUMZUPZWKVAUUKWKUUKVAUUA
      UUKWLWKUUKXCUUAUUKWMWNYPUUHYRUUIYTUUJYPUUHWMWNYOUDCWOWPWQYRUUIWMWNYQUFBUN
      UFAAWRWHWSWPWQYTUUJWMWNYSVQGUOVQAAWTWJWSWPWQXAXBXDXEXFVOUUFVRFUMZVBLUMZUT
      UUAUUCUULUUEUUMUUBVRFUSVRAAXGWAWSWPUUDVBLUQVBAAXHVOWSWPXMXIXJVCVCUDUGURUE
      VEVCVKXKXLXNXOUEXPXQUFXRXQUEYNVEVFXSUFXTYAYBUEUFYCVMUEYDXQUFYDXQYNYDXQXOW
      HUEUFYEVMZHUUGUJYFULZMUMZUJYGULZKUMZUJYHULZDUMZUPZVAUUGVCMUMZUEKUMZYNDUMZ
      UPZVAUAUVAUVEUUGUVAUVEWMWNUUPUVBUURUVCUUTUVDUUPUVBWMWNUUOVCMYFVCAAYIVPWSW
      PWQUURUVCWMWNUUQUEKYGUEAAYJXOWSWPWQUUTUVDWMWNUUSYNDYHYNAAYKUUNWSWPWQXAXBX
      IYLXEYM $.
      $( [3-Jan-2015] $)

    $( Structure product base set. $)
    prdmbaselem $p |- ( ( S e. W /\ R e. Z ) -> B = ( Base ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cbs cfv cprdm co wceq prdmbaseex prdmvalex prdmvalfun baseid
      cvv elex cnx cop cplusg cmulr ctp csca cvsca cpr cun cts cple tpid1 elun1
      cds opex mp2b eleqtrri strfv 3syl adantl prdmval fveq2d eqtr4d ) FNUEZEOU
      EZUFZCHUGUHZFEUIUJZUGUHWBCWDUKZWAWBEUPUECUPUEWFEOUQACEUPPULCHUGUPUDABCDEF
      GHIJKLMPQRSTUAUBUCUMABCDEFGHIJKLMPQRSTUAUBUCUNUDUOURUGUHZCUSZWHURUTUHBUSZ
      URVAUHGUSZVBZURVCUHFUSURVDUHLUSVEZVFZURVGUHMUSURVHUHKUSURVKUHDUSVBZVFZHWH
      WKUEWHWMUEWHWOUEWHWIWJWGCVLVIWHWKWLVJWHWMWNVJVMUCVNVOVPVQWCWEHUGABCDEFGHI
      JKLMNOPQRSTUAUBUCVRVSVT $.
      $( [3-Jan-2015] $)

    $( Structure product addition. $)
    prdmplusglem $p |- ( ( S e. W /\ R e. Z ) -> A = ( +g ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cplusg cfv cprdm cvv wceq prdmbaseex cdm cmpt mpt2exg anidms
      co cv syl adantl prdmvalex prdmvalfun plusgid cnx cop cbs cmulr ctp cvsca
      csca cpr cun cts cple opex tpid2 elun1 mp2b eleqtrri strfv prdmval fveq2d
      cds eqtr4d ) FNUEZEOUEZUFZBHUGUHZFEUIUQZUGUHWGBUJUEZBWHUKWFWJWEWFCUJUEZWJ
      ACEOPULWKWJIJCCAEUMAURZIURUHWLJURUHWLEUHUGUHUQUNUJUJBQUOUPUSUTBHUGUJUDABC
      DEFGHIJKLMPQRSTUAUBUCVAABCDEFGHIJKLMPQRSTUAUBUCVBUDVCVDUGUHZBVEZVDVFUHCVE
      ZWNVDVGUHGVEZVHZVDVJUHFVEVDVIUHLVEVKZVLZVDVMUHMVEVDVNUHKVEVDWCUHDVEVHZVLZ
      HWNWQUEWNWSUEWNXAUEWOWNWPWMBVOVPWNWQWRVQWNWSWTVQVRUCVSVTUSWGWIHUGABCDEFGH
      IJKLMNOPQRSTUAUBUCWAWBWD $.
      $( [3-Jan-2015] $)

    $( Structure product multiplication. $)
    prdmmulrlem $p |- ( ( S e. W /\ R e. Z ) -> T = ( .r ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cmulr cfv cprdm co cvv wceq prdmbaseex adantl cdm cv mpt2exg
      cmpt anidms syl prdmvalex prdmvalfun mulrid cnx cop cbs cplusg csca cvsca
      ctp cpr cun cts cple opex tpid3 elun1 mp2b eleqtrri prdmval fveq2d eqtr4d
      cds strfv ) FNUEZEOUEZUFZGHUGUHZFEUIUJZUGUHWGGUKUEZGWHULWGCUKUEZWJWFWKWEA
      CEOPUMUNWKWJIJCCAEUOAUPZIUPUHWLJUPUHWLEUHUGUHUJURUKUKGRUQUSUTGHUGUKUDABCD
      EFGHIJKLMPQRSTUAUBUCVAABCDEFGHIJKLMPQRSTUAUBUCVBUDVCVDUGUHZGVEZVDVFUHCVEZ
      VDVGUHBVEZWNVJZVDVHUHFVEVDVIUHLVEVKZVLZVDVMUHMVEVDVNUHKVEVDWCUHDVEVJZVLZH
      WNWQUEWNWSUEWNXAUEWOWPWNWMGVOVPWNWQWRVQWNWSWTVQVRUCVSWDUTWGWIHUGABCDEFGHI
      JKLMNOPQRSTUAUBUCVTWAWB $.
      $( [3-Jan-2015] $)

    $( Structure product scalars. $)
    prdmscalem $p |- ( ( S e. W /\ R e. Z ) ->
        S = ( Scalar ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa csca cfv cprdm wceq simpl prdmvalex prdmvalfun scaid cnx cop
      co cbs cplusg cmulr ctp cvsca cpr cun cts cple cds opex prid1 elun2 elun1
      mp2b eleqtrri strfv syl prdmval fveq2d eqtr4d ) FNUEZEOUEZUFZFHUGUHZFEUIU
      QZUGUHWAVSFWBUJVSVTUKFHUGNUDABCDEFGHIJKLMPQRSTUAUBUCULABCDEFGHIJKLMPQRSTU
      AUBUCUMUDUNUOUGUHZFUPZUOURUHCUPUOUSUHBUPUOUTUHGUPVAZWEUOVBUHLUPZVCZVDZUOV
      EUHMUPUOVFUHKUPUOVGUHDUPVAZVDZHWEWHUEWEWIUEWEWKUEWEWGWDFVHVIWEWHWFVJWEWIW
      JVKVLUCVMVNVOWAWCHUGABCDEFGHIJKLMNOPQRSTUAUBUCVPVQVR $.
      $( [3-Jan-2015] $)

    $( Structure product scalar multiplication. $)
    prdmvscalem $p |- ( ( S e. W /\ R e. Z ) ->
        M = ( vsca ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cvsca cfv cprdm co wceq cvv prdmbaseex cbs fvex cmpt mpt2exg
      cdm cv mpan prdmvalex prdmvalfun vscaid cnx cop cplusg cmulr ctp csca cpr
      cun cts cple cds opex prid2 elun2 elun1 mp2b eleqtrri 3syl adantl prdmval
      strfv fveq2d eqtr4d ) FNUEZEOUEZUFZLHUGUHZFEUIUJZUGUHWHLWJUKZWGWHCULUEZLU
      LUEZWLACEOPUMFUNUHZULUEWMWNFUNUOIJWOCAEURIUSAUSZJUSUHWPEUHUGUHUJUPULULLSU
      QUTLHUGULUDABCDEFGHIJKLMPQRSTUAUBUCVAABCDEFGHIJKLMPQRSTUAUBUCVBUDVCVDUGUH
      ZLVEZVDUNUHCVEVDVFUHBVEVDVGUHGVEVHZVDVIUHFVEZWRVJZVKZVDVLUHMVEVDVMUHKVEVD
      VNUHDVEVHZVKZHWRXAUEWRXBUEWRXDUEWTWRWQLVOVPWRXAWSVQWRXBXCVRVSUCVTWDWAWBWI
      WKHUGABCDEFGHIJKLMNOPQRSTUAUBUCWCWEWF $.
      $( [3-Jan-2015] $)

    $( Structure product topology. $)
    prdmtopsetlem $p |- ( ( S e. W /\ R e. Z ) ->
        O = ( TopOpen ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cprdm co cts cfv prdmval fveq2d cvv wceq ccom fvex prdmvalex
      cpt eqeltri prdmvalfun topsetid cnx cop cbs cplusg cmulr ctp csca cpr cun
      cvsca cple cds opex tpid1 elun2 ax-mp eleqtrri strfv syl6reqr ) FNUEEOUEU
      FZFEUGUHZUIUJHUIUJZMWAWBHUIABCDEFGHIJKLMNOPQRSTUAUBUCUKULMUMUEMWCUNMUIEUO
      ZURUJUMTWDURUPUSMHUIUMUDABCDEFGHIJKLMPQRSTUAUBUCUQABCDEFGHIJKLMPQRSTUAUBU
      CUTUDVAVBUIUJZMVCZVBVDUJCVCVBVEUJBVCVBVFUJGVCVGVBVHUJFVCVBVKUJLVCVIVJZWFV
      BVLUJKVCZVBVMUJDVCZVGZVJZHWFWJUEWFWKUEWFWHWIWEMVNVOWFWJWGVPVQUCVRVSVQVT
      $.
      $( [3-Jan-2015] $)

    $( Structure product weak ordering. $)
    prdmlelem $p |- ( ( S e. W /\ R e. Z ) -> L = ( le ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cple cfv cprdm co cvv wceq cxp wss cv cpr wbr cdm wral copab
      vex prss biimpri adantr ssopab2i df-xp 3sstr4i prdmbaseex xpexg syl ssexg
      anidms sylancr adantl prdmvalex prdmvalfun pleid cnx cop cbs cplusg cmulr
      ctp csca cvsca cun cts cds opex tpid2 elun2 ax-mp eleqtrri prdmval fveq2d
      strfv eqtr4d ) FNUEZEOUEZUFZKHUGUHZFEUIUJZUGUHWTKUKUEZKXAULWSXCWRWSKCCUMZ
      UNXDUKUEZXCIUOZJUOZUPCUNZAUOZXFUHXIXGUHXIEUHUGUHUQAEURUSZUFZIJUTXFCUEXGCU
      EUFZIJUTKXDXKXLIJXHXLXJXLXHXFXGCIVAJVAVBVCVDVEUAIJCCVFVGWSCUKUEZXEACEOPVH
      XMXECCUKUKVIVLVJKXDUKVKVMVNKHUGUKUDABCDEFGHIJKLMPQRSTUAUBUCVOABCDEFGHIJKL
      MPQRSTUAUBUCVPUDVQVRUGUHZKVSZVRVTUHCVSVRWAUHBVSVRWBUHGVSWCVRWDUHFVSVRWEUH
      LVSUPWFZVRWGUHMVSZXOVRWHUHDVSZWCZWFZHXOXSUEXOXTUEXQXOXRXNKWIWJXOXSXPWKWLU
      CWMWPVJWTXBHUGABCDEFGHIJKLMNOPQRSTUAUBUCWNWOWQ $.
      $( [3-Jan-2015] $)

    $( Structure product metric. $)
    prdmdistlem $p |- ( ( S e. W /\ R e. Z ) -> D = ( dist ` ( S Xm_ R ) ) ) $=
      ( vr wcel wa cds cfv cprdm co cvv wceq prdmbaseex adantl cdm cmpt crn cxr
      cv clt csup mpt2exg anidms prdmvalex prdmvalfun dsid cnx cop cplusg cmulr
      cbs ctp csca cvsca cpr cun cts cple opex tpid3 elun2 ax-mp eleqtrri strfv
      3syl prdmval fveq2d eqtr4d ) FNUEZEOUEZUFZDHUGUHZFEUIUJZUGUHWKCUKUEZDUKUE
      ZDWLULWJWNWIACEOPUMUNWNWOIJCCAEUOAUSZIUSUHWPJUSUHWPEUHUGUHUJUPUQURUTVAUKU
      KDUBVBVCDHUGUKUDABCDEFGHIJKLMPQRSTUAUBUCVDABCDEFGHIJKLMPQRSTUAUBUCVEUDVFV
      GUGUHZDVHZVGVKUHCVHVGVIUHBVHVGVJUHGVHVLVGVMUHFVHVGVNUHLVHVOVPZVGVQUHMVHZV
      GVRUHKVHZWRVLZVPZHWRXBUEWRXCUEWTXAWRWQDVSVTWRXBWSWAWBUCWCWDWEWKWMHUGABCDE
      FGHIJKLMNOPQRSTUAUBUCWFWGWH $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d P x f g $.  $d S x f g $.  $d R x f g $.  $d B x f g $.
    prdmbase.p $e |- P = ( S Xm_ R ) $.
    $( Base set of a structure product. $)
    prdmbase $p |- ( ( S e. V /\ R e. W ) ->
        X_ x e. dom R ( Base ` ( R ` x ) ) = ( Base ` P ) ) $=
      ( vf vg wcel cvv cv cfv cbs co cmpt cmpt2 cnx cop eqid cdm cixp wceq elex
      wa cprdm cplusg cds crn cxr clt csup cmulr ctp csca cvsca cpr cun cts cpt
      ccom cple wss wbr wral copab prdmbaselem fveq2i syl6eqr syl2an ) DEJDKJZC
      KJZACUAZALZCMZNMUBZBNMZUCCFJDEUDCFUDVKVLUEVPDCUFOZNMVQAHIVPVPAVMVNHLZMZVN
      ILZMZVOUGMOPQZVPHIVPVPAVMVTWBVOUHMOPUIUJUKULQZCDHIVPVPAVMVTWBVOUMMOPQZRNM
      VPSRUGMWCSRUMMWESUNRUOMDSRUPMHIDNMVPAVMVSWBVOUPMOPQZSUQURRUSMUSCVAUTMZSRV
      BMVSWAUQVPVCVTWBVOVBMVDAVMVEUEHIVFZSRUHMWDSUNURZHIWHWFWGKKVPTWCTWETWFTWGT
      WHTWDTWITVGBVRNGVHVIVJ $.
      $( [3-Jan-2015] $)

    $( Scalar ring of a structure product. $)
    prdmsca $p |- ( ( S e. V /\ R e. W ) -> S = ( Scalar ` P ) ) $=
      ( vx vf vg wcel cvv csca cfv co cv cmpt cmpt2 cnx cop eqid wceq cprdm cdm
      elex wa cbs cixp cplusg cds crn cxr clt csup cmulr ctp cvsca cpr cun ccom
      cts cpt cple wss wbr wral copab prdmscalem fveq2i syl6eqr syl2an ) CDJCKJ
      ZBKJZCALMZUABEJCDUDBEUDVKVLUECCBUBNZLMVMGHIGBUCZGOZBMZUFMUGZVRGVOVPHOZMZV
      PIOZMZVQUHMNPQZVRHIVRVRGVOVTWBVQUIMNPUJUKULUMQZBCHIVRVRGVOVTWBVQUNMNPQZRU
      FMVRSRUHMWCSRUNMWESUORLMCSRUPMHICUFMVRGVOVSWBVQUPMNPQZSUQURRUTMUTBUSVAMZS
      RVBMVSWAUQVRVCVTWBVQVBMVDGVOVEUEHIVFZSRUIMWDSUOURZHIWHWFWGKKVRTWCTWETWFTW
      GTWHTWDTWITVGAVNLFVHVIVJ $.
      $( [5-Jan-2015] $)

    prdmplusg.b $e |- B = ( Base ` P ) $.
    $( Addition in a structure product. $)
    prdmplusg $p |- ( ( S e. V /\ R e. W ) ->
        ( f e. B , g e. B |-> ( x e. dom R |->
          ( ( f ` x ) ( +g ` ( R ` x ) ) ( g ` x ) ) ) ) = ( +g ` P ) ) $=
      ( wcel cvv cfv cplusg co cmpt2 cnx cop eqid cdm cv cmpt wceq elex wa cixp
      cbs cprdm cds crn cxr clt csup cmulr ctp csca cvsca cpr cun cts ccom cple
      cpt wss wral copab prdmplusglem prdmbase syl6reqr mpt2eq12 syl2anc fveq2i
      wbr a1i 3eqtr4d syl2an ) EHLEMLZDMLZFGBBADUAZAUBZFUBZNZWAGUBZNZWADNZONPUC
      ZQZCONZUDDILEHUEDIUEVRVSUFZFGAVTWFUHNUGZWKWGQZEDUIPZONZWHWIAWLWKFGWKWKAVT
      WCWEWFUJNPUCUKULUMUNQZDEFGWKWKAVTWCWEWFUONPUCQZRUHNWKSRONWLSRUONWPSUPRUQN
      ESRURNFGEUHNWKAVTWBWEWFURNPUCQZSUSUTRVANVADVBVDNZSRVCNWBWDUSWKVEWCWEWFVCN
      VNAVTVFUFFGVGZSRUJNWOSUPUTZFGWSWQWRMMWKTWLTWPTWQTWRTWSTWOTWTTVHWJBWKUDZXA
      WHWLUDWJWKCUHNBACDEMMJVIKVJZXBFGBBWKWKWGVKVLWIWNUDWJCWMOJVMVOVPVQ $.
      $( [3-Jan-2015] $)

    ${
      $d K f g $.
      prdmvsca.k $e |- K = ( Base ` S ) $.
      $( Scalar multiplication in a structure product. $)
      prdmvsca $p |- ( ( S e. V /\ R e. W ) ->
          ( f e. K , g e. B |-> ( x e. dom R |->
            ( f ( vsca ` ( R ` x ) ) ( g ` x ) ) ) ) = ( vsca ` P ) ) $=
        ( cvv cfv cvsca co cnx cop eqid wcel cdm cv cmpt cmpt2 wceq elex wa cbs
        cixp a1i prdmbase syl6reqr mpt2eq12 syl2anc cplusg cds crn cxr clt csup
        cprdm cmulr ctp csca cpr cun cts ccom cpt cple wss wbr wral prdmvscalem
        copab eqtrd fveq2i syl6eqr syl2an ) EIUAENUAZDNUAZFGHBADUBZFUCZAUCZGUCZ
        OZWEDOZPOQUDZUEZCPOZUFDJUAEIUGDJUGWAWBUHZWJEDVBQZPOZWKWLWJFGEUIOZAWCWHU
        IOUJZWIUEZWNWLHWOUFZBWPUFWJWQUFWRWLMUKWLWPCUIOBACDENNKULLUMFGHBWOWPWIUN
        UOAFGWPWPAWCWEWDOZWGWHUPOQUDUEZWPFGWPWPAWCWSWGWHUQOQUDURUSUTVAUEZDEFGWP
        WPAWCWSWGWHVCOQUDUEZRUIOWPSRUPOWTSRVCOXBSVDRVEOESRPOWQSVFVGRVHOVHDVIVJO
        ZSRVKOWDWFVFWPVLWSWGWHVKOVMAWCVNUHFGVPZSRUQOXASVDVGZFGXDWQXCNNWPTWTTXBT
        WQTXCTXDTXATXETVOVQCWMPKVRVSVT $.
        $( [5-Jan-2015] $)
    $}
  $}

  ${
    $d x F $.
    $( Indexed cross product elements are functions. $)
    ixpfn $p |- ( F e. X_ x e. I J -> F Fn I ) $=
      ( cixp wcel cvv wfn cv cfv wral elixp2 simp2bi ) BACDEFBGFBCHAIBJDFACKACD
      BLM $.
      $( [4-Jan-2015] $)
  $}

  ${
    $d I x y z $.  $d J y z $.  $d K y z $.
    $( Condition for an explicit member of an indexed product. $)
    mptelixpg $p |- ( I e. V -> ( ( x e. I |-> J ) e. X_ x e. I K <->
          A. x e. I J e. K ) ) $=
      ( vy vz wcel cvv cmpt cixp wral cv cfv wa ax-17 fvmpt2 ralimiaa wi syl wb
      wfn csb w3a hbcsb1 csbeq1a cbvixp eleq2i elixp2 3anass 3bitri eqid1 fnmpt
      elex wel simpr eqeltrd jca wf dffn2 eqid eleq1d biimpd ralim sylbir sylbi
      fmpt imp impbii hbmpt1 hbfv hbel fveq2 eleq12d cbvral anbi2i bitri mptexg
      weq biantrurd syl5rbb syl5bb ) BEHBIHZABCJZABDKZHZCDHZABLZUABEUNWFWDIHZWD
      BUBZFMZWDNZAWKDUCZHZFBLZOZOZWCWHWFWDFBWMKZHWIWJWOUDWQWEWRWDAFGGBDWMGMDHFP
      AGWKDGFUOAPZUEZAWKDUFZUGUHFBWMWDUIWIWJWOUJUKWHWPWCWQWHWJAMZWDNZDHZABLZOZW
      PWHXFWHWJXEABCWDDWDULZUMWGXDABXBBHZWGOXCCDABCDWDXGQXHWGUPUQRURWJXEWHWJBIW
      DUSZXEWHSZBWDUTXICIHZABLZXJABICWDWDVAVGXLXDWGSZABLXJXKXMABXHXKOZXDWGXNXCC
      DABCIWDXGQVBVCRXDWGABVDTVEVFVHVIXEWOWJXDWNAFBXDFPAGGWLWMAGWKWDAGBCVJWSVKW
      TVLAFVSXCWLDWMXBWKWDVMXAVNVOVPVQWCWIWPABCIVRVTWAWBT $.
      $( [4-Jan-2015] $)
  $}

  ${
    $d S x $.  $d R x $.  $d B x $.  $d P x $.  $d X x $.  $d F x $.
    prdmbaseprj.b $e |- B = ( Base ` P ) $.
    prdmbaseprj.p $e |- P = ( S Xm_ R ) $.
    $( Projection closure in a structure product. $)
    prdmbaseprj $p |- ( ( ( S e. V /\ R e. W ) /\ ( F e. B /\ X e. dom R ) ) ->
        ( F ` X ) e. ( Base ` ( R ` X ) ) ) $=
      ( vx wcel wa cdm cv cfv cbs wral simprr fveq2 cixp syl6eqr eleq2d biimpar
      prdmbase cvv wfn elixp2 simp3bi syl adantrr wceq eleq12d rcla4va syl2anc
      fveq2d ) DFLCGLMZEALZHCNZLZMMUTKOZEPZVACPZQPZLZKUSRZHEPZHCPZQPZLZUQURUTSU
      QURVFUTUQURMEKUSVDUAZLZVFUQVLURUQVKAEUQVKBQPAKBCDFGJUEIUBUCUDVLEUFLEUSUGV
      FKUSVDEUHUIUJUKVEVJKHUSVAHULZVBVGVDVIVAHETVMVCVHQVAHCTUPUMUNUO $.
      $( [5-Jan-2015] $)

    $( Structure product elements are tuples. $)
    prdmbasefn $p |- ( ( S e. V /\ R e. W /\ F e. B ) -> F Fn dom R ) $=
      ( vx wcel w3a cdm cv cfv cbs cixp wfn simp3 syl6eleq prdmbase 3adant3 cvv
      wceq eleqtrrd wral elixp2 simp2bi syl ) DFKZCGKZEAKZLZEJCMZJNZCOPOZQZKZEU
      NRZUMEBPOZUQUMEAUTUJUKULSHTUJUKUQUTUDULJBCDFGIUAUBUEUREUCKUSUOEOUPKJUNUFJ
      UNUPEUGUHUI $.
      $( [5-Jan-2015] $)

    $( Membership of a tuple in a structure product. $)
    prdmbasempt $p |- ( ( S e. V /\ R e. W ) -> ( ( x e. dom R |-> U ) e. B <->
        A. x e. dom R U e. ( Base ` ( R ` x ) ) ) ) $=
      ( wcel wa cdm cmpt cv cfv cbs cixp wral cvv eleq2d dmexg adantl mptelixpg
      prdmbase syl6reqr wb syl bitrd ) EGKZDHKZLZADMZFNZBKUNAUMAODPQPZRZKZFUOKA
      UMSZULBUPUNULUPCQPBACDEGHJUEIUFUAULUMTKZUQURUGUKUSUJDHUBUCAUMFUOTUDUHUI
      $.
      $( [5-Jan-2015] $)
  $}

  ${
    $d S x f g $.  $d R x f g $.  $d B x f g $.  $d G x f g $.  $d F x f g $.
    $d Y x f g $.
    prdmplusgval.b $e |- B = ( Base ` Y ) $.
    prdmplusgval.p $e |- P = ( +g ` Y ) $.
    prdmplusgval.y $e |- Y = ( S Xm_ R ) $.
    $( Value of addition in a structure product. $)
    prdmplusgval $p |- ( ( ( S e. V /\ R e. W ) /\ ( F e. B /\ G e. B ) ) ->
        ( F P G ) = ( x e. dom R |->
          ( ( F ` x ) ( +g ` ( R ` x ) ) ( G ` x ) ) ) ) $=
      ( vf vg wcel wa cvv cfv wceq co cdm cv cplusg cmpt elex anim12i prdmplusg
      cmpt2 adantr eqcomd syl5eq oveqan12d adantl mpteq2dv simprl simprr mptexg
      fveq1 dmexg syl ad2antlr ovmpt2d sylan ) EHPZDIPZQERPZDRPZQZFBPZGBPZQZFGC
      UAADUBZAUCZFSZVNGSZVNDSUDSZUAZUEZTVEVGVFVHEHUFDIUFUGVIVLQZNOFGBBAVMVNNUCZ
      SZVNOUCZSZVQUAZUEZVSCRVTCJUDSZNOBBWFUIZLVTWHWGVIWHWGTVLABJDENORRMKUHUJUKU
      LVTWAFTZWCGTZQZQAVMWEVRWKWEVRTVTWIWJWBVOWDVPVQVNWAFUSVNWCGUSUMUNUOVIVJVKU
      PVIVJVKUQVHVSRPZVGVLVHVMRPWLDRUTAVMVRRURVAVBVCVD $.
      $( [3-Jan-2015] $)

    $d V x f g $.  $d W x f g $.
    $( Closure of addition in a structure product. $)
    prdmplusgcl $p |- ( ( ( S e. V /\ R e. W /\
        A. x e. dom R ( R ` x ) e. Grp ) /\
          ( F e. B /\ G e. B ) ) -> ( F P G ) e. B ) $=
      ( wcel cfv wral wa co 3adantl3 ax-17 cv cgrp cdm cplusg cmpt prdmplusgval
      w3a wceq cbs hbra1 hb3an hban ra4 imp 3ad2antl3 adantrl adantrlr adantrll
      prdmbaseprj eqid grpcl syl3anc expr ralrimi wb prdmbasempt 3adant3 adantr
      mpbird eqeltrd ) EHNZDINZAUAZDOZUBNZADUCZPZUGZFBNZGBNZQZQZFGCRZAVPVMFOZVM
      GOZVNUDOZRZUEZBVKVLWAWCWHUHVQABCDEFGHIJKLMUFSWBWHBNZWGVNUIOZNZAVPPZWBWKAV
      PVRWAAVKVLVQAVKATVLATVOAVPUJUKWAATULVRWAVMVPNZWKVRWAWMQZQVOWDWJNZWEWJNZWK
      VRWMVOWAVQVKWMVOVLVQWMVOVOAVPUMUNUOUPVKVLWNWOVQVKVLQZVSWMWOVTBJDEFHIVMKMU
      SUQSVKVLWNWPVQWQVTWMWPVSBJDEGHIVMKMUSURSWJWFVNWDWEWJUTWFUTVAVBVCVDVRWIWLV
      EZWAVKVLWRVQABJDEWGHIKMVFVGVHVIVJ $.
      $( [6-Jan-2015] $)

    $d I x f g $.  $d N x g $.
    prdmidlem.i $e |- I = ( x e. dom R |-> ( 0g ` ( R ` x ) ) ) $.
    $( Characterization of identity in a structure product. $)
    prdmidlem $p |- ( ( S e. V /\ R e. W /\
        A. x e. dom R ( R ` x ) e. Grp ) ->
          ( I e. B /\ A. f e. B ( I P f ) = f ) ) $=
      ( wcel cfv wral co wceq cmpt cv cgrp cdm w3a c0g cbs eqid1 grpidcl ralimi
      3ad2ant3 wb prdmbasempt 3adant3 mpbird syl5eqel wa ax-17 hbra1 hb3an hban
      cplusg cvv fvex fvmpt2 adantl oveq1d simpl3 r19.21bi prdmbaseprj 3adantl3
      mpan2 anassrs eqid grplid syl2anc eqtrd simpl1 simpl2 adantr prdmplusgval
      mpteq2da simpr syl22anc prdmbasefn syl3anc dffn5v sylib 3eqtr4d ralrimiva
      wfn jca ) EHOZDIOZAUAZDPZUBOZADUCZQZUDZGBOZGFUAZCRZXASZFBQWSGAWQWOUEPZTZB
      NWSXEBOZXDWOUFPZOZAWQQZWRWLXIWMWPXHAWQXGWOXDXGUGXDUGUHUIUJWLWMXFXIUKWRABJ
      DEXDHIKMULUMUNUOZWSXCFBWSXABOZUPZAWQWNGPZWNXAPZWOVAPZRZTZAWQXNTZXBXAXLAWQ
      XPXNWSXKAWLWMWRAWLAUQWMAUQWPAWQURUSXKAUQUTXLWNWQOZUPZXPXDXNXORZXNXTXMXDXN
      XOXSXMXDSZXLXSXDVBOYBWOUEVCAWQXDVBGNVDVKVEVFXTWPXNXGOZYAXNSXLWPAWQWLWMWRX
      KVGVHWSXKXSYCWLWMXKXSUPYCWRBJDEXAHIWNKMVIVJVLXGXOWOXDXNXGVMXOVMXDVMVNVOVP
      WAXLWLWMWTXKXBXQSWLWMWRXKVQZWLWMWRXKVRZWSWTXKXJVSWSXKWBZABCDEGXAHIJKLMVTW
      CXLXAWQWJZXAXRSXLWLWMXKYGYDYEYFBJDEXAHIKMWDWEAWQXAWFWGWHWIWK $.
      $( [6-Jan-2015] $)

    prdminvlem.n $e |- N = ( x e. dom R |->
        ( ( invg ` ( R ` x ) ) ` ( F ` x ) ) ) $.
    $( Characterization of inverses in a structure product. $)
    prdminvlem $p |- ( ( ( S e. V /\ R e. W /\
        A. x e. dom R ( R ` x ) e. Grp ) /\ F e. B ) ->
          ( N e. B /\ ( N P F ) = I ) ) $=
      ( wcel cfv wceq eqid cv cgrp cdm wral w3a wa cminusg cmpt cbs ax-17 hbra1
      co hb3an simpl3 r19.21bi prdmbaseprj 3adantl3 anassrs grpinvcl syl2anc ex
      hban ralrimi wb simpl1 simpl2 prdmbasempt mpbird syl5eqel cplusg c0g fvex
      cvv fvmpt2 mpan2 adantl oveq1d eqtrd mpteq2da simpr prdmplusgval syl22anc
      grplinv a1i 3eqtr4d jca ) EIQZDJQZAUAZDRZUBQZADUCZUDZUEZFBQZUFZHBQZHFCULZ
      GSWPHAWLWIFRZWJUGRZRZUHZBPWPXBBQZXAWJUIRZQZAWLUDZWPXEAWLWNWOAWGWHWMAWGAUJ
      WHAUJWKAWLUKUMWOAUJVBZWPWIWLQZXEWPXHUFZWKWSXDQZXEWPWKAWLWGWHWMWOUNUOZWNWO
      XHXJWGWHWOXHUFXJWMBKDEFIJWILNUPUQURZXDWJWTWSXDTZWTTZUSUTVAVCWPWGWHXCXFVDW
      GWHWMWOVEZWGWHWMWOVFZABKDEXAIJLNVGUTVHVIZWPAWLWIHRZWSWJVJRZULZUHZAWLWJVKR
      ZUHZWRGWPAWLXTYBXGXIXTXAWSXSULZYBXIXRXAWSXSXHXRXASZWPXHXAVMQYEWSWTVLAWLXA
      VMHPVNVOVPVQXIWKXJYDYBSXKXLXDXSWJWTYBWSXMXSTYBTXNWCUTVRVSWPWGWHWQWOWRYASX
      OXPXQWNWOVTABCDEHFIJKLMNWAWBGYCSWPOWDWEWF $.
      $( [6-Jan-2015] $)
  $}

  ${
    $d S x f g $.  $d R x f g $.  $d B x f g $.  $d G x f g $.  $d F x f g $.
    $d Y x f g $.  $d K x f g $.
    prdmvscaval.b $e |- B = ( Base ` Y ) $.
    prdmvscaval.m $e |- M = ( vsca ` Y ) $.
    prdmvscaval.k $e |- K = ( Base ` S ) $.
    prdmvscaval.y $e |- Y = ( S Xm_ R ) $.
    $( Value of scalar multiplication in a structure product. $)
    prdmvscaval $p |- ( ( ( S e. V /\ R e. W ) /\ ( F e. K /\ G e. B ) ) ->
        ( F M G ) = ( x e. dom R |->
          ( F ( vsca ` ( R ` x ) ) ( G ` x ) ) ) ) $=
      ( wcel wa cvv cfv wceq vf vg co cv cvsca cmpt elex anim12i cmpt2 prdmvsca
      cdm eqcomd syl5eq adantr id1 fveq1 oveqan12d mpteq2dv adantl simprl dmexg
      simprr mptexg syl ad2antlr ovmpt2d sylan ) DIPZCJPZQDRPZCRPZQZEGPZFBPZQZE
      FHUCACUKZEAUDZFSZVQCSUESZUCZUFZTVHVJVIVKDIUGCJUGUHVLVOQZUAUBEFGBAVPUAUDZV
      QUBUDZSZVSUCZUFZWAHRVLHUAUBGBWGUIZTVOVLHKUESZWHMVLWHWIABKCDUAUBGRROLNUJUL
      UMUNWCETZWDFTZQZWGWATWBWLAVPWFVTWJWKWCEWEVRVSWJUOVQWDFUPUQURUSVLVMVNUTVLV
      MVNVBVKWARPZVJVOVKVPRPWMCRVAAVPVTRVCVDVEVFVG $.
      $( [5-Jan-2015] $)

    $( Closure of scalar multiplication in a structure product. $)
    prdmvscacl $p |- ( ( ( S e. V /\ R e. W /\
        A. x e. dom R ( ( R ` x ) e. LMod /\ ( Scalar ` ( R ` x ) ) = S ) ) /\
          ( F e. K /\ G e. B ) ) -> ( F M G ) e. B ) $=
      ( wcel cfv wa cbs cvv cv clmod csca wceq cdm wral w3a co cmpt prdmvscaval
      cvsca 3adantl3 elex ax-17 hbra1 hb3an hban simpl3 simprr ra4 sylc simprll
      simpld simprd syl6eqr eleqtrrd prdmbaseprj adantrll eqid lmodvscl syl3anc
      id fveq2d expr ralrimi syl3anl prdmbasempt 3adant3 adantr mpbird eqeltrd
      wb ) DIPZCJPZAUAZCQZUBPZWFUCQZDUDZRZACUEZUFZUGZEGPZFBPZRZRZEFHUHZAWKEWEFQ
      ZWFUKQZUHZUIZBWCWDWPWRXBUDWLABCDEFGHIJKLMNOUJULWQXBBPZXAWFSQZPZAWKUFZWCDT
      PZWDCTPZWLWLWPXFDIUMCJUMWLVLXGXHWLUGZWPRXEAWKXIWPAXGXHWLAXGAUNXHAUNWJAWKU
      OUPWPAUNUQXIWPWEWKPZXEXIWPXJRZRZWGEWHSQZPWSXDPZXEXLWGWIXLWLXJWJXGXHWLXKUR
      XIWPXJUSWJAWKUTVAZVCXLEGXMXIWNWOXJVBXLXMDSQGXLWHDSXLWGWIXOVDVMNVEVFXIWOXJ
      XNWNXGXHWOXJRXNWLBKCDFTTWELOVGULVHEWTWHXMXDWFWSXDVIWHVIWTVIXMVIVJVKVNVOVP
      WMXCXFWBZWPWCWDXPWLABKCDXAIJLOVQVRVSVTWA $.
      $( [5-Jan-2015] $)
  $}

  ${
    $d P y z $.  $d S x y z $.  $d I x y z $.  $d R y z $.
    prdmbase2.p $e |- P = ( S Xm_ ( x e. I |-> R ) ) $.
    $( Base set of a structure product, explicit function. $)
    prdmbase2 $p |- ( ( S e. V /\ I e. W /\ A. x e. I R e. Z ) ->
        X_ x e. I ( Base ` R ) = ( Base ` P ) ) $=
      ( vy vz wcel cvv wral cbs cfv cixp wceq elex cv ralimi w3a cmpt cdm simp3
      wfn eqid fnmpt fndm 3syl ixpeq1 syl simp1 3ad2ant2 prdmbase syl2anc ax-17
      mptexg hbmpt1 wel hbfv weq fveq2d cbvixp wa fvmpt2 ralimiaa ixpeq2 syl5eq
      fveq2 3ad2ant3 3eqtr3rd syl3an ) DFLDMLZEGLEMLZCHLZAENCMLZAENZAECOPZQZBOP
      ZRDFSEGSVPVQAECHSUAVNVOVRUBZJAECUCZUDZJTZWCPZOPZQZJEWGQZWAVTWBWDERZWHWIRW
      BVRWCEUFWJVNVOVRUEAECWCMWCUGZUHEWCUIUJJWDEWGUKULWBVNWCMLZWHWARVNVOVRUMVOV
      NWLVRAECMURUNJBWCDMMIUOUPVRVNWIVTRVOVRWIAEATZWCPZOPZQZVTJAKKEWGWOAKWFOKTZ
      OLAUQAKWEWCAKECUSKJUTAUQVAVAWQWOLJUQJAVBWFWNOWEWMWCVJVCVDVRWOVSRZAENWPVTR
      VQWRAEWMELVQVEWNCOAECMWCWKVFVCVGAEWOVSVHULVIVKVLVM $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d S a b c x y z $.  $d R a b c x y z $.  $d X a b c x y z $.
    $d ph a b c y z $.

    ${
      prdmgrpd.s $e |- ( ph -> S e. V ) $.
      prdmgrpd.r $e |- ( ph -> R e. W ) $.
      prdmgrpd.g $e |- ( ph -> A. x e. dom R ( R ` x ) e. Grp ) $.
      $( The product of a family of groups is a group. $)
      prdmgrpd $p |- ( ph -> ( S Xm_ R ) e. Grp ) $=
        ( va vy co cfv cv cmpt wcel cvv eqid wceq syl22anc vb vc cbs cplusg cdm
        cprdm cminusg c0g eqidd cgrp wral elex 3ad2ant1 simp2 simp3 prdmplusgcl
        w3a syl syl32anc wa simpr ad2antrr fveq2 eleq1d rcla4va syl2anc simplr1
        prdmbaseprj simplr2 simplr3 grpass syl13anc adantr simpr1 simpr2 fveq1d
        prdmplusgval fvmpt2 mpan2 adantl oveq1d simpr3 oveq2d 3eqtr4d mpteq2dva
        eqtrd fveq2d cbvmptv prdmidlem syl3anc simpld wi simprd ra4 imp fveq12d
        ovex 3jca prdminvlem sylan isgrpd ) AJUAUBDCUFLZUCMZXBUDMZXBKCUEZKNZJNZ
        MZXFCMZUGMZMZOZKXEXIUHMZOZAXCUIAXDUIAXGXCPZUANZXCPZUQDQPZCQPZBNZCMZUJPZ
        BXEUKZXOXQXGXPXDLZXCPZAXOXRXQADEPXRGDEULURZUMAXOXSXQACFPXSHCFULURZUMAXO
        YCXQIUMAXOXQUNAXOXQUOBXCXDCDXGXPQQXBXCRZXDRZXBRZUPZUSAXOXQUBNZXCPZUQZUT
        ZKXEXFYDMZXFYLMZXIUDMZLZOZKXEXHXFXPYLXDLZMZYRLZOZYDYLXDLZXGUUAXDLZYOKXE
        YSUUCYOXFXEPZUTZXHXFXPMZYRLZYQYRLZXHUUIYQYRLZYRLZYSUUCUUHXIUJPZXHXIUCMZ
        PZUUIUUOPZYQUUOPZUUKUUMSUUHUUGYCUUNYOUUGVAZAYCYNUUGIVBYBUUNBXFXEXTXFSYA
        XIUJXTXFCVCVDVEVFUUHXRXSXOUUGUUPAXRYNUUGYFVBZAXSYNUUGYGVBZXOXQYMAUUGVGU
        USXCXBCDXGQQXFYHYJVHTUUHXRXSXQUUGUUQUUTUVAXOXQYMAUUGVIUUSXCXBCDXPQQXFYH
        YJVHTUUHXRXSYMUUGUURUUTUVAXOXQYMAUUGVJUUSXCXBCDYLQQXFYHYJVHTUUOYRXIXHUU
        IYQUUORYRRVKVLUUHYPUUJYQYRUUHYPXFKXEUUJOZMZUUJUUHXFYDUVBYOYDUVBSZUUGYOX
        RXSXOXQUVDAXRYNYFVMZAXSYNYGVMZAXOXQYMVNZAXOXQYMVOZKXCXDCDXGXPQQXBYHYIYJ
        VQTVMVPUUGUVCUUJSZYOUUGUUJQPUVIXHUUIYRWQKXEUUJQUVBUVBRVRVSVTWFWAUUHUUBU
        ULXHYRUUHUUBXFKXEUULOZMZUULUUHXFUUAUVJYOUUAUVJSZUUGYOXRXSXQYMUVLUVEUVFU
        VHAXOXQYMWBZKXCXDCDXPYLQQXBYHYIYJVQTVMVPUUGUVKUULSZYOUUGUULQPUVNUUIYQYR
        WQKXEUULQUVJUVJRVRVSVTWFWCWDWEYOXRXSYEYMUUEYTSUVEUVFYOXRXSYCXOXQYEUVEUV
        FAYCYNIVMZUVGUVHYKUSUVMKXCXDCDYDYLQQXBYHYIYJVQTYOXRXSXOUUAXCPZUUFUUDSUV
        EUVFUVGYOXRXSYCXQYMUVPUVEUVFUVOUVHUVMBXCXDCDXPYLQQXBYHYIYJUPUSKXCXDCDXG
        UUAQQXBYHYIYJVQTWDAXNXCPZXNXGXDLXGSZJXCUKZAXRXSYCUVQUVSUTYFYGIBXCXDCDJX
        NQQXBYHYIYJKBXEXMYAUHMXFXTSZXIYAUHXFXTCVCZWGWHZWIWJZWKAXOUVRAUVSXOUVRWL
        AUVQUVSUWCWMUVRJXCWNURWOAXOUTZXLXCPZXLXGXDLXNSZAXRXSYCUQXOUWEUWFUTAXRXS
        YCYFYGIWRBXCXDCDXGXNXLQQXBYHYIYJUWBKBXEXKXTXGMZYAUGMZMUVTXHUWGXJUWHUVTX
        IYAUGUWAWGXFXTXGVCWPWHWSWTZWKUWDUWEUWFUWIWMXA $.
        $( [6-Jan-2015] $)
    $}

    $( The product of a family of groups is a group. $)
    prdmgrp $p |- ( ( S e. V /\ R e. W /\ A. x e. dom R ( R ` x ) e. Grp ) ->
        ( S Xm_ R ) e. Grp ) $=
      ( wcel cv cfv cgrp cdm wral w3a simp1 simp2 simp3 prdmgrpd ) CDFZBEFZAGBH
      IFABJKZLABCDEQRSMQRSNQRSOP $.
      $( [6-Jan-2015] $)

    $( Zero in a product of groups. $)
    prdm0g $p |- ( ( S e. V /\ R e. W /\ A. x e. dom R ( R ` x ) e. Grp ) ->
        ( y e. dom R |-> ( 0g ` ( R ` y ) ) ) = ( 0g ` ( S Xm_ R ) ) ) $=
      ( va wcel cvv cv cfv cgrp cdm wral c0g co wceq elex id eqid cprdm w3a cbs
      cmpt cplusg fveq2 fveq2d cbvmptv prdmidlem simpld simprd oveq2 eqeq12d wb
      rcla4v sylc prdmgrp grpid syl2anc mpbid eqcomd syl3an ) DEHDIHZCFHCIHZAJZ
      CKZLHACMZNZVHBVGBJZCKZOKZUDZDCUAPZOKZQDERCFRVHSVCVDVHUBZVNVLVOVLVLVMUEKZP
      ZVLQZVNVLQZVOVLVMUCKZHZVLGJZVPPZWBQZGVTNZVRVOWAWEAVTVPCDGVLIIVMVTTZVPTZVM
      TBAVGVKVFOKVIVEQVJVFOVIVECUFUGUHUIZUJZVOWAWEWHUKWDVRGVLVTWBVLQZWCVQWBVLWB
      VLVLVPULWJSUMUOUPVOVMLHWAVRVSUNACDIIUQWIVTVPVMVNVLWFWGVNTURUSUTVAVB $.
      $( [4-Jan-2015] $)

    $( Negation in a product of groups. $)
    prdminvg $p |- ( ( ( S e. V /\ R e. W /\ A. x e. dom R ( R ` x ) e.
            Grp ) /\ X e. ( Base ` ( S Xm_ R ) ) ) ->
          ( ( invg ` ( S Xm_ R ) ) ` X ) = ( y e. dom R |->
            ( ( invg ` ( R ` y ) ) ` ( X ` y ) ) ) ) $=
      ( wcel cvv cv cfv cgrp co cminusg cmpt wceq elex c0g eqid fveq2 cdm cprdm
      wral cbs id w3a wa cplusg fveq2d cbvmptv fveq12d prdminvlem simprd prdm0g
      weq adantr eqtrd wb prdmgrp simpr simpld grpinvid2 syl3anc mpbird syl3anl
      ) DEHDIHZCFHCIHZAJZCKZLHACUAZUCZVKGDCUBMZUDKZHZGVLNKZKBVJBJZGKZVPCKZNKZKZ
      OZPZDEQCFQVKUEVFVGVKUFZVNUGZWBWAGVLUHKZMZVLRKZPZWDWFBVJVRRKZOZWGWDWAVMHZW
      FWJPZAVMWECDGWJWAIIVLVMSZWESZVLSBAVJWIVIRKBAUOZVRVIRVPVHCTZUIUJBAVJVTVHGK
      ZVINKZKWOVQWQVSWRWOVRVINWPUIVPVHGTUKUJULZUMWCWJWGPVNABCDIIUNUPUQWDVLLHZVN
      WKWBWHURWCWTVNACDIIUSUPWCVNUTWDWKWLWSVAVMWEVLVOWGGWAWMWNWGSVOSVBVCVDVE $.
      $( [4-Jan-2015] $)

    $( The product of a family of Abelian groups is Abelian. $)
    prdmabl $p |- ( ( S e. V /\ R e. W /\ A. x e. dom R ( R ` x ) e. Abel ) ->
        ( S Xm_ R ) e. Abel ) $=
      ( va vb wcel cvv cv cfv cabel wral co elex w3a ax-17 wceq eqid syl22anc
      cdm cprdm cbs cplusg eqidd cgrp ablgrp ralimi prdmgrp syl3an3 hbra1 hb3an
      id cmpt wa simpl13 simpr simpl11 simpl12 simpl2 prdmbaseprj simpl3 ablcom
      ra4 sylc syl3anc mpteq2da simp11 simp12 simp2 prdmplusgval 3eqtr4d isabld
      simp3 syl3an ) CDHCIHZBEHBIHZAJZBKZLHZABUAZMZWBCBUBNZLHCDOBEOWBUMVPVQWBPZ
      FGWCUCKZWCUDKZWCWDWEUEWDWFUEWBVPVQVSUFHZAWAMWCUFHVTWGAWAVSUGUHABCIIUIUJWD
      FJZWEHZGJZWEHZPZAWAVRWHKZVRWJKZVSUDKZNZUNZAWAWNWMWONZUNZWHWJWFNZWJWHWFNZW
      LAWAWPWRWDWIWKAVPVQWBAVPAQVQAQVTAWAUKULWIAQWKAQULWLVRWAHZUOZVTWMVSUCKZHZW
      NXDHZWPWRRXCWBXBVTVPVQWBWIWKXBUPWLXBUQZVTAWAVDVEXCVPVQWIXBXEVPVQWBWIWKXBU
      RZVPVQWBWIWKXBUSZWDWIWKXBUTXGWEWCBCWHIIVRWESZWCSZVATXCVPVQWKXBXFXHXIWDWIW
      KXBVBXGWEWCBCWJIIVRXJXKVATXDWOVSWMWNXDSWOSVCVFVGWLVPVQWIWKWTWQRVPVQWBWIWK
      VHZVPVQWBWIWKVIZWDWIWKVJZWDWIWKVNZAWEWFBCWHWJIIWCXJWFSZXKVKTWLVPVQWKWIXAW
      SRXLXMXOXNAWEWFBCWJWHIIWCXJXPXKVKTVLVMVO $.
      $( [5-Jan-2015] $)

    ${
      prdmlmodd.s $e |- ( ph -> S e. Ring ) $.
      prdmlmodd.r $e |- ( ph -> R e. W ) $.
      prdmlmodd.m $e |- ( ph -> A. x e. dom R ( ( R ` x ) e. LMod /\
          ( Scalar ` ( R ` x ) ) = S ) ) $.
      $( The product of a family of left modules is a left module. $)
      prdmlmodd $p |- ( ph -> ( S Xm_ R ) e. LMod ) $=
        ( vy co cfv crg wcel cvv wceq eqid wa adantr cmpt syl22anc va vb cplusg
        vc cprdm cbs cvsca cmulr cur eqidd csca elex syl prdmsca syl2anc cv cdm
        cgrp clmod weq fveq2 eleq1d fveq2d eqeq1d anbi12d cbvralv sylib lmodgrp
        ralimi prdmgrp syl3anc w3a 3ad2ant1 simp2 simp3 prdmvscacl syl32anc ra4
        imp simpld sylan adantlr simplr1 simprd eqcomd eleqtrd ad2antrr simplr2
        wral prdmbaseprj simplr3 lmodvsdi1 syl112anc simpr2 simpr3 prdmplusgval
        simpr fveq1d ovex fvmpt2 mpan2 adantl oveq2d simpr1 prdmvscaval oveq12d
        eqtrd 3eqtr4d mpteq2dva grpcl lmodvsdi2 syl121anc oveq1d sylancl rngacl
        oveqd a1i lmodvsass rngcl simplr lmodvs1 rngidcl wfn prdmbasefn islmodd
        dffn5v ) AUAUBUDDCUEJZUCKZDUFKZDUCKZYGUGKZDUHKZDUIKZDYGUFKZYGAYNUJAYHUJ
        ADLMZCNMZDYGUKKOFACEMYPGCEULUMZYGCDLNYGPZUNUOAYKUJAYIUJAYJUJAYLUJAYMUJF
        AYOYPIUPZCKZURMZICUQZWIZYGURMZFYQAYTUSMZYTUKKZDOZQZIUUBWIZUUCABUPZCKZUS
        MZUUKUKKZDOZQZBUUBWIUUIHUUOUUHBIUUBBIUTZUULUUEUUNUUGUUPUUKYTUSUUJYSCVAZ
        VBUUPUUMUUFDUUPUUKYTUKUUQVCVDVEVFVGZUUHUUAIUUBUUEUUAUUGYTVHRVIUMICDLNVJ
        VKZAUAUPZYIMZUBUPZYNMZVLYOYPUUIUVAUVCUUTUVBYKJZYNMZAUVAYOUVCFVMAUVAYPUV
        CYQVMAUVAUUIUVCUURVMAUVAUVCVNAUVAUVCVOIYNCDUUTUVBYIYKLNYGYNPZYKPZYIPZYR
        VPZVQAUVAUVCUDUPZYNMZVLZQZIUUBUUTYSUVBUVJYHJZKZYTUGKZJZSZIUUBYSUVDKZYSU
        UTUVJYKJZKZYTUCKZJZSZUUTUVNYKJZUVDUVTYHJZUVMIUUBUVQUWCUVMYSUUBMZQZUUTYS
        UVBKZYSUVJKZUWBJZUVPJZUUTUWIUVPJZUUTUWJUVPJZUWBJZUVQUWCUWHUUEUUTUUFUFKZ
        MZUWIYTUFKZMZUWJUWRMZUWLUWOOAUWGUUEUVLAUUIUWGUUEUURUUIUWGQZUUEUUGUUIUWG
        UUHUUHIUUBVRVSZVTWAZWBUWHUUTYIUWPUVAUVCUVKAUWGWCZAUWGYIUWPOUVLAUWGQZDUU
        FUFUXEUUFDAUUIUWGUUGUURUXAUUEUUGUXBWDWAWEZVCWBWFUWHYOYPUVCUWGUWSAYOUVLU
        WGFWGZAYPUVLUWGYQWGZUVAUVCUVKAUWGWHUVMUWGWQZYNYGCDUVBLNYSUVFYRWJTUWHYOY
        PUVKUWGUWTUXGUXHUVAUVCUVKAUWGWKZUXIYNYGCDUVJLNYSUVFYRWJZTUWBUUTUVPUUFUW
        PUWRYTUWIUWJUWRPZUWBPZUUFPZUVPPZUWPPZWLWMUWHUVOUWKUUTUVPUWHUVOYSIUUBUWK
        SZKZUWKUVMUVOUXROUWGUVMYSUVNUXQUVMYOYPUVCUVKUVNUXQOAYOUVLFRZAYPUVLYQRZA
        UVAUVCUVKWNZAUVAUVCUVKWOZIYNYHCDUVBUVJLNYGUVFYHPZYRWPTWRRUWGUXRUWKOZUVM
        UWGUWKNMUYDUWIUWJUWBWSIUUBUWKNUXQUXQPWTXAXBXGXCUWHUVSUWMUWAUWNUWBUWHUVS
        YSIUUBUWMSZKZUWMUWHYSUVDUYEUVMUVDUYEOZUWGUVMYOYPUVAUVCUYGUXSUXTAUVAUVCU
        VKXDZUYAIYNCDUUTUVBYIYKLNYGUVFUVGUVHYRXETRWRUWGUYFUWMOZUVMUWGUWMNMUYIUU
        TUWIUVPWSIUUBUWMNUYEUYEPWTXAXBXGUWHUWAYSIUUBUWNSZKZUWNUWHYSUVTUYJUWHYOY
        PUVAUVKUVTUYJOZUXGUXHUXDUXJIYNCDUUTUVJYIYKLNYGUVFUVGUVHYRXEZTWRUWGUYKUW
        NOZUVMUWGUWNNMZUYNUUTUWJUVPWSZIUUBUWNNUYJUYJPWTZXAXBXGXFXHXIUVMYOYPUVAU
        VNYNMZUWEUVROUXSUXTUYHUVMUUDUVCUVKUYRAUUDUVLUUSRUYAUYBYNYHYGUVBUVJUVFUY
        CXJVKIYNCDUUTUVNYIYKLNYGUVFUVGUVHYRXETUVMYOYPUVEUVTYNMZUWFUWDOUXSUXTUVM
        YOYPUUIUVAUVCUVEUXSUXTAUUIUVLUURRZUYHUYAUVIVQUVMYOYPUUIUVAUVKUYSUXSUXTU
        YTUYHUYBIYNCDUUTUVJYIYKLNYGUVFUVGUVHYRVPZVQIYNYHCDUVDUVTLNYGUVFUYCYRWPT
        XHAUVAUVBYIMZUVKVLZQZIUUBUUTUVBYJJZUWJUVPJZSZIUUBUWAYSUVBUVJYKJZKZUWBJZ
        SZVUEUVJYKJZUVTVUHYHJZVUDIUUBVUFVUJVUDUWGQZUUTUVBUUFUCKZJZUWJUVPJZUWNUV
        BUWJUVPJZUWBJZVUFVUJVUNUUEUWQUVBUWPMZUWTVUQVUSOAUWGUUEVUCUXCWBZVUNUUTYI
        UWPUVAVUBUVKAUWGWCZVUNDUUFUFAUWGDUUFOVUCUXFWBZVCZWFZVUNUVBYIUWPUVAVUBUV
        KAUWGWHZVVDWFZVUNYOYPUVKUWGUWTAYOVUCUWGFWGZAYPVUCUWGYQWGZUVAVUBUVKAUWGW
        KZVUDUWGWQZUXKTZUWBVUOUUTUVBUVPUUFUWPUWRYTUWJUXLUXMUXNUXOUXPVUOPXKXLVUN
        VUEVUPUWJUVPVUNYJVUOUUTUVBVUNDUUFUCVVCVCXPXMVUNUWAUWNVUIVURUWBVUNUWAUYK
        UWNVUNYSUVTUYJVUNYOYPUVAUVKUYLVVHVVIVVBVVJUYMTWRVUNUWGUYOUYNVVKUYPUYQXN
        XGVUNVUIYSIUUBVURSZKZVURVUNYSVUHVVMVUNYOYPVUBUVKVUHVVMOVVHVVIVVFVVJIYNC
        DUVBUVJYIYKLNYGUVFUVGUVHYRXETWRVUNUWGVURNMZVVNVUROVVKVVOVUNUVBUWJUVPWSX
        QIUUBVURNVVMVVMPWTUOXGZXFXHXIVUDYOYPVUEYIMZUVKVULVUGOAYOVUCFRZAYPVUCYQR
        ZVUDYOUVAVUBVVQVVRAUVAVUBUVKXDZAUVAVUBUVKWNZYIYJDUUTUVBUVHYJPXOVKAUVAVU
        BUVKWOZIYNCDVUEUVJYIYKLNYGUVFUVGUVHYRXETVUDYOYPUYSVUHYNMZVUMVUKOVVRVVSV
        UDYOYPUUIUVAUVKUYSVVRVVSAUUIVUCUURRZVVTVWBVUAVQVUDYOYPUUIVUBUVKVWCVVRVV
        SVWDVWAVWBIYNCDUVBUVJYIYKLNYGUVFUVGUVHYRVPVQZIYNYHCDUVTVUHLNYGUVFUYCYRW
        PTXHVUDIUUBUUTUVBYLJZUWJUVPJZSZIUUBUUTVUIUVPJZSZVWFUVJYKJZUUTVUHYKJZVUD
        IUUBVWGVWIVUNUUTUVBUUFUHKZJZUWJUVPJZUUTVURUVPJZVWGVWIVUNUUEUWQVUTUWTVWO
        VWPOVVAVVEVVGVVLUUTUVBUVPVWMUUFUWPUWRYTUWJUXLUXNUXOUXPVWMPXRXLVUNVWFVWN
        UWJUVPVUNYLVWMUUTUVBVUNDUUFUHVVCVCXPXMVUNVUIVURUUTUVPVVPXCXHXIVUDYOYPVW
        FYIMZUVKVWKVWHOVVRVVSVUDYOUVAVUBVWQVVRVVTVWAYIDYLUUTUVBUVHYLPXSVKVWBIYN
        CDVWFUVJYIYKLNYGUVFUVGUVHYRXETVUDYOYPUVAVWCVWLVWJOVVRVVSVVTVWEIYNCDUUTV
        UHYIYKLNYGUVFUVGUVHYRXETXHAUUTYNMZQZIUUBYMYSUUTKZUVPJZSZIUUBVWTSZYMUUTY
        KJZUUTVWSIUUBVXAVWTVWSUWGQZVXAUUFUIKZVWTUVPJZVWTVXEYMVXFVWTUVPAUWGYMVXF
        OVWRUXEDUUFUIUXFVCWBXMVXEUUEVWTUWRMZVXGVWTOAUWGUUEVWRUXCWBVXEYOYPVWRUWG
        VXHAYOVWRUWGFWGAYPVWRUWGYQWGAVWRUWGXTVWSUWGWQYNYGCDUUTLNYSUVFYRWJTUVPVX
        FUUFUWRYTVWTUXLUXNUXOVXFPYAUOXGXIVWSYOYPYMYIMZVWRVXDVXBOAYOVWRFRZAYPVWR
        YQRZAVXIVWRAYOVXIFYIDYMUVHYMPYBUMRAVWRWQZIYNCDYMUUTYIYKLNYGUVFUVGUVHYRX
        ETVWSUUTUUBYCZUUTVXCOVWSYOYPVWRVXMVXJVXKVXLYNYGCDUUTLNUVFYRYDVKIUUBUUTY
        FVGXHYE $.
        $( [5-Jan-2015] $)
    $}
  $}

