$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Additional theory of functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b B $.
    $( A mapping operation with empty domain.  (Contributed by Stefan O'Rear,
       29-Jan-2015.) $)
    mpt20 $p |- ( a e. (/) , b e. B |-> X ) = (/) $=
      ( cmpt2 wfn wceq cxp cvv wcel wral ral0 eqid fnmpt2 mpbi xp0r fneq2i fn0
      c0 ) CDSABEZSFZTSGTSAHZFZUABIJDAKZCSKUCUDCLCDSABTTMNOUBSTAPQOTRO $.
      $( [29-Jan-2015] $)
  $}

  ${
    $d x A $.  $d x C $.  $d x D $.  $d x V $.
    fvmpt3.a $e |- ( x = A -> B = C ) $.
    fvmpt3.b $e |- F = ( x e. D |-> B ) $.
    fvmpt3.c $e |- ( x e. D -> B e. V ) $.
    $( Value of a function given in maps-to notation, with a slightly different
       sethood condition.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    fvmpt3 $p |- ( A e. D -> ( F ` A ) = C ) $=
      ( wcel cfv wceq cv eleq1d vtoclga fvmptg mpdan ) BEKDGKZBFLDMCGKSABEANBMC
      DGHOJPABCDEGFHIQR $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d F a $.  $d X a $.

    $( Two possibilities for the behavior of a function value.  (Contributed by
       Stefan O'Rear, 2-Nov-2014.) $)
    fvbr0 $p |- ( X F ( F ` X ) \/ ( F ` X ) = (/) ) $=
      ( va cv csn cima wcel weu cfv wbr c0 wceq wo cio wsbc iota4 eqcomi dfsbcq
      wb dffv3 ax-mp fvex eleq1 sbcie bitri biimpi elimasni 3syl orcd wn syl5eq
      iotanul olcd pm2.61i ) CDZABEFZGZCHZBBAIZAJZUSKLZMURUTVAURUQCUQCNZOZUSUPG
      ZUTUQCPVCVDVCUQCUSOZVDVBUSLVCVESUSVBCBATZQUQCVBUSRUAUQVDCUSBAUBUOUSUPUCUD
      UEUFABUSUGUHUIURUJZVAUTVGUSVBKVFUQCULUKUMUN $.
      $( [2-Nov-2014] $)
  $}

  $( All function values are either null or in the range.  (Contributed by
     Stefan O'Rear, 3-Jan-2015.) $)
  fvelrn0 $p |- ( F ` X ) e. ( ran F u. { (/) } ) $=
    ( cvv wcel cfv crn c0 csn cun wbr wceq wo fvbr0 wa simpr fvex simpl brelrng
    a1i syl3anc elun1 syl 0ex snid elun2 ax-mp eleq1 mpbiri adantr jaoian fvprc
    mpan wn syl6eqel pm2.61i ) BCDZBAEZAFZGHZIZDZBUQAJZUQGKZLUPVAABMVBUPVAVCVBU
    PNZUQURDZVAVDUPUQCDZVBVEVBUPOVFVDBAPSVBUPQBUQACCRTUQURUSUAUBVCVAUPVCVAGUTDZ
    GUSDVGGUCUDGUSURUEUFZUQGUTUGUHUIUJULUPUMUQGUTBAUKVHUNUO $.
    $( [3-Jan-2015] $)


  $( The result of a function value is always a subset of the union of the
     range, even if it is invalid and thus empty.  (Contributed by Stefan
     O'Rear, 2-Nov-2014.) $)
  fvssunirn $p |- ( F ` X ) C_ U. ran F $=
    ( cfv crn wcel c0 csn wo cuni wss cun fvelrn0 elun elssuni 0ss elsni sseq1d
    mpbi mpbiri jaoi ax-mp ) BACZADZEZUBFGZEZHZUBUCIZJZUBUCUEKEUGABLUBUCUEMRUDU
    IUFUBUCNUFUIFUHJUHOUFUBFUHUBFPQSTUA $.
    $( [30-Jan-2015] $)

  ${
    $d x A $.  $d x I $.  $d x F $.
    $( Membership in a union of some function-defined family of sets.
       (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    fnunirn $p |- ( F Fn I -> ( A e. U. ran F <->
        E. x e. I A e. ( F ` x ) ) ) $=
      ( wfn crn cuni wcel cv cfv cdm wrex wfun fnfun elunirn fndm rexeqdv bitrd
      wb syl ) CDEZBCFGHZBAICJHZACKZLZUCADLUACMUBUESDCNABCOTUAUCAUDDDCPQR $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d ph y $.  $d ps x $.  $d F x y $.  $d B x y $.  $d A x y $.
    rexima.x $e |- ( x = ( F ` y ) -> ( ph <-> ps ) ) $.
    $( Existential quantification under an image in terms of the base set.
       _EDITORIAL_: usable in ~ fnwe2lem2 .  (Contributed by Stefan O'Rear,
       21-Jan-2015.) $)
    rexima $p |- ( ( F Fn A /\ B C_ A ) ->
        ( E. x e. ( F " B ) ph <-> E. y e. B ps ) ) $=
      ( wfn wss wa cv cfv cima fvex wcel wceq wrex fvelimab eqcom rexbii syl6bb
      wb adantl rexxfr2d ) GEIFEJKZABCDDLZGMZGFNZFUGGOUFCLZUIPUHUJQZDFRUJUHQZDF
      RDEFUJGSUKULDFUHUJTUAUBULABUCUFHUDUE $.
      $( [21-Jan-2015] $)

    $( Universal quantification under an image in terms of the base set.
       _EDITORIAL_: duplicates ~ raleqfn , but with interface consistency with
       ~ rexrn .  (Contributed by Stefan O'Rear, 21-Jan-2015.) $)
    ralima $p |- ( ( F Fn A /\ B C_ A ) ->
        ( A. x e. ( F " B ) ph <-> A. y e. B ps ) ) $=
      ( wfn wss wa cv cfv cima fvex wcel wceq wrex fvelimab eqcom rexbii syl6bb
      wb adantl ralxfr2d ) GEIFEJKZABCDDLZGMZGFNZFUGGOUFCLZUIPUHUJQZDFRUJUHQZDF
      RDEFUJGSUKULDFUHUJTUAUBULABUCUFHUDUE $.
      $( [21-Jan-2015] $)
  $}

  $( Images under a function never map nonempty sets to empty sets.
     _EDITORIAL_: usable in ~ fnwe2lem2 .  (Contributed by Stefan O'Rear,
     21-Jan-2015.) $)
  fnimaeq0 $p |- ( ( F Fn A /\ B C_ A ) ->
      ( ( F " B ) = (/) <-> B = (/) ) ) $=
    ( cima c0 wceq cdm cin wfn wa imadisj incom fndm sseq2d biimpar df-ss sylib
    wss syl5eq eqeq1d syl5bb ) CBDEFCGZBHZEFCAIZBARZJZBEFCBKUFUCBEUFUCBUBHZBUBB
    LUFBUBRZUGBFUDUHUEUDUBABACMNOBUBPQSTUA $.
    $( [21-Jan-2015] $)
  ${
    $d F x y $.  $d G x y $.  $d A x y $.
    $( Two ways to express the locus of differences between two functions.
       (Contributed by Stefan O'Rear, 17-Jan-2015.) $)
    fndmdif $p |- ( ( F Fn A /\ G Fn A ) -> dom ( F \ G ) =
          { x e. A | ( F ` x ) =/= ( G ` x ) } ) $=
      ( vy wfn wa cdif cdm cv cfv wceq copab dffn5 biimpi wn wex cab wi annim
      wcel wne crab adantr adantl difeq12d dmeqd difopab dmopab imdistan bicomi
      dmeqi notbii anbi2i bitr2i 3bitri exbii 19.42v eqeq1 notbid ceqsexv df-ne
      fvex bitr4i abbii df-rab eqtr4i 3eqtri syl6eq ) CBFZDBFZGZCDHZIAJZBUAZEJZ
      VNCKZLZGZAEMZVOVPVNDKZLZGZAEMZHZIZVQWAUBZABUCZVLVMWEVLCVTDWDVJCVTLZVKVJWI
      AEBCNOUDVKDWDLZVJVKWJAEBDNOUEUFUGWFVSWCPGZAEMZIWKEQZARZWHWEWLVSWCAEUHULWK
      AEUIWNVOWGGZARWHWMWOAWMVOVRWBPZGZGZEQVOWQEQZGWOWKWREWKVSWCSZPVOVRWBSZSZPZ
      WRVSWCTWTXBXBWTVOVRWBUJUKUMWRVOXAPZGXCWQXDVOVRWBTUNVOXATUOUPUQVOWQEURWSWG
      VOWSVQWALZPZWGWPXFEVQVNCVCVRWBXEVPVQWAUSUTVAVQWAVBVDUNUPVEWGABVFVGVHVI $.
      $( [17-Jan-2015] $)

    $( The difference set between two functions is commutative.  (Contributed
       by Stefan O'Rear, 17-Jan-2015.) $)
    fndmdifcom $p |- ( ( F Fn A /\ G Fn A ) ->
        dom ( F \ G ) = dom ( G \ F ) ) $=
      ( vx wfn wa cv cfv wne crab cdif cdm wb wcel necom rabbiia fndmdif ancoms
      a1i wceq 3eqtr4a ) BAEZCAEZFDGZBHZUDCHZIZDAJUFUEIZDAJZBCKLCBKLZUGUHDAUGUH
      MUDANUEUFOSPDABCQUCUBUJUITDACBQRUA $.
      $( [17-Jan-2015] $)

    $( The difference set of two functions is empty if and only if the
       functions are equal.  (Contributed by Stefan O'Rear, 17-Jan-2015.) $)
    fndmdifeq0 $p |- ( ( F Fn A /\ G Fn A ) ->
        ( dom ( F \ G ) = (/) <-> F = G ) ) $=
      ( vx wfn wa cv cfv wne crab c0 wceq wral cdif cdm wb wn rabeq0 nne ralbii
      bitri a1i fndmdif eqeq1d eqfnfv2 3bitr4d ) BAECAEFZDGZBHZUHCHZIZDAJZKLZUI
      UJLZDAMZBCNOZKLBCLUMUOPUGUMUKQZDAMUOUKDARUQUNDAUIUJSTUAUBUGUPULKDABCUCUDD
      ABCUEUF $.
      $( [17-Jan-2015] $)

    $( Two ways to express the locus of equality between two functions.
       (Contributed by Stefan O'Rear, 17-Jan-2015.) $)
    fndmin $p |- ( ( F Fn A /\ G Fn A ) -> dom ( F i^i G ) =
          { x e. A | ( F ` x ) = ( G ` x ) } ) $=
      ( vy wfn wa cin cdm wcel cfv wceq copab crab dffn5 biimpi syl6eq wex cab
      cv ineqan12d inopab dmeqd 19.42v anandi exbii fvex ceqsexv anbi2i 3bitr3i
      eqeq1 abbii dmopab df-rab 3eqtr4i ) CBFZDBFZGZCDHZIATZBJZETZUTCKZLZGZVAVB
      UTDKZLZGZGZAEMZIZVCVFLZABNZURUSVJURUSVEAEMZVHAEMZHVJUPUQCVNDVOUPCVNLAEBCO
      PUQDVOLAEBDOPUAVEVHAEUBQUCVIERZASVAVLGZASVKVMVPVQAVAVDVGGZGZERVAVRERZGVPV
      QVAVREUDVSVIEVAVDVGUEUFVTVLVAVGVLEVCUTCUGVBVCVFUKUHUIUJULVIAEUMVLABUNUOQ
      $.
      $( [17-Jan-2015] $)
  $}

  ${
    $d F x y $.  $d A x y $.  $d X x y $.
    $( Express the class of fixed points of a function.  (Contributed by Stefan
       O'Rear, 1-Feb-2015.) $)
    fninfp $p |- ( F Fn A -> dom ( F i^i _I ) = { x e. A | ( F ` x ) = x } ) $=
      ( wfn cid cin cdm cres cv cfv wceq crab inres incom reseq1i eqtri 3eqtr4i
      fnresdm ineq1d syl5reqr dmeqd fnresi fndmin mpan2 wcel fvresi rabbiia a1i
      eqeq2d 3eqtrd ) CBDZCEFZGCEBHZFZGZAIZCJZUPUMJZKZABLZUQUPKZABLZUKULUNUKUNC
      BHZEFZULEVCFZULBHZVDUNVEECFZBHVFECBMVGULBECNOPVCENCEBMQUKVCCEBCRSTUAUKUMB
      DUOUTKBUBABCUMUCUDUTVBKUKUSVAABUPBUEURUPUQBUPUFUIUGUHUJ $.
      $( [1-Feb-2015] $)

    $( Property of a fixed point of a function.  (Contributed by Stefan O'Rear,
       1-Feb-2015.) $)
    fnelfp $p |- ( ( F Fn A /\ X e. A ) -> ( X e. dom ( F i^i _I ) <->
          ( F ` X ) = X ) ) $=
      ( vx wfn cid cin cdm wcel cv cfv wceq crab fninfp eleq2d fveq2 id eqeq12d
      elrab3 sylan9bb ) BAEZCBFGHZICDJZBKZUCLZDAMZICAICBKZCLZUAUBUFCDABNOUEUHDC
      AUCCLZUDUGUCCUCCBPUIQRST $.
      $( [1-Feb-2015] $)
  $}


  $( Constant function with loose range.  (Contributed by Stefan O'Rear,
     1-Feb-2015.) $)
  fconst6g $p |- ( B e. C -> ( A X. { B } ) : A --> C ) $=
    ( wcel csn cxp wf wss fconstg snssi fss syl2anc ) BCDABEZAMFZGMCHACNGABCIBC
    JAMCNKL $.
    $( [1-Feb-2015] $)

  ${
    $d x A $.  $d x F $.  $d x B $.
    $( Inverse images under functions expressed as abstractions.  (Contributed
       by Stefan O'Rear, 1-Feb-2015.) $)
    fncnvima2 $p |- ( F Fn A -> ( `' F " B ) = { x e. A | ( F ` x ) e. B } ) $=
      ( wfn cv ccnv cima wcel cab cfv crab elpreima abbidv abid2 eqcomi 3eqtr4g
      wa df-rab ) DBEZAFZDGCHZIZAJZUABIUADKCIZRZAJUBUEABLTUCUFABUACDMNUDUBAUBOP
      UEABSQ $.
      $( [1-Feb-2015] $)

    $( Inverse point images under functions expressed as abstractions.
       (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    fniniseg2 $p |- ( F Fn A -> ( `' F " { B } ) =
          { x e. A | ( F ` x ) = B } ) $=
      ( wfn ccnv csn cima cv cfv wcel crab wceq fncnvima2 wb fvex elsnc rabbiia
      a1i syl6eq ) DBEDFCGZHAIZDJZUAKZABLUCCMZABLABUADNUDUEABUDUEOUBBKUCCUBDPQS
      RT $.
      $( [1-Feb-2015] $)

    $( Support sets of functions expressed as abstractions.  (Contributed by
       Stefan O'Rear, 1-Feb-2015.) $)
    fnniniseg2 $p |- ( F Fn A -> ( `' F " ( _V \ { B } ) ) =
          { x e. A | ( F ` x ) =/= B } ) $=
      ( wfn ccnv cvv csn cdif cima cv cfv wcel wne fncnvima2 wb eldifsn mpbiran
      crab fvex a1i rabbiia syl6eq ) DBEDFGCHIZJAKZDLZUDMZABSUFCNZABSABUDDOUGUH
      ABUGUHPUEBMUGUFGMUHUEDTUFGCQRUAUBUC $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d F a $.  $d V a $.  $d A a $.  $d B a $.  $d Z a $.
    $( Two ways to express restriction of a support set.  (Contributed by
       Stefan O'Rear, 5-Feb-2015.) $)
    fnsuppres $p |- ( ( F Fn ( A u. B ) /\ ( A i^i B ) = (/) /\ Z e. V ) ->
        ( ( `' F " ( _V \ { Z } ) ) C_ A <-> ( F |` B ) = ( B X. { Z } ) ) ) $=
      ( va cun wfn wceq wcel cfv crab wss wral wa wn wb a1i syl5bb syl2anc cres
      cin w3a wne csn cxp ccnv cvv cdif cima unss ssrab2 biantrur rabun2 sseq1i
      c0 cv 3bitr4ri wi rabss fvres adantl fvconst2g 3ad2antl3 eqeq12d id simp2
      nne minel syl2anr mtt syl 3bitr2rd ralbidva fnniniseg2 sseq1d simp1 ssun2
      3ad2ant1 fnssres fnconstg 3ad2ant3 eqfnfv2 3bitr4d ) CABGZHZABUBUPIZEDJZU
      CZFUQZCKZEUDZFWELZAMZWJCBUAZKZWJBEUEZUFZKZIZFBNZCUGUHWQUIUJZAMWOWRIZWNWLF
      BLZAMZWIXAWLFALZAMZXEOXFXDGZAMXEWNXFXDAUKXGXEWLFAULUMWMXHAWLFABUNUOURXEWL
      WJAJZUSZFBNWIXAWLFBAUTWIXJWTFBWIWJBJZOZWTWKEIZWLPZXJXLWPWKWSEXKWPWKIWIWJB
      CVAVBWHWFXKWSEIWGBEWJDVCVDVEXNXMQXLWKEVHRXLXIPZXNXJQXKXKWGXOWIXKVFWFWGWHV
      GWJBAVIVJXIWLVKVLVMVNSSWIXBWMAWFWGXBWMIWHFWEECVOVSVPWIWOBHZWRBHZXCXAQWIWF
      BWEMZXPWFWGWHVQXRWIBAVRRWEBCVTTWHWFXQWGBEDWAWBFBWOWRWCTWD $.
      $( [5-Feb-2015] $)
  $}

  ${
    rngel1ne0.o $e |- O = ( 1r ` R ) $.
    rngel1ne0.z $e |- Z = ( 0g ` R ) $.
    rngel1ne0.b $e |- B = ( Base ` R ) $.
    $( A ring is nonzero if it has a nonzero element.  (Contributed by Stefan
       O'Rear, 6-Feb-2015.) $)
    rngel1ne0 $p |- ( ( R e. Ring /\ X e. ( B \ { Z } ) ) -> O =/= Z ) $=
      ( crg wcel csn cdif wa wne eldifsn simprbi adantl wceq wi simpl rng1eq0
      eldifi rng0cl adantr syl3anc necon3d mpd ) BIJZDAEKZLJZMZDENZCENUJULUHUJD
      AJZULDAEOPQUKCEDEUKUHUMEAJZCERDERSUHUJTUJUMUHDAUIUBQUHUNUJABEHGUCUDABCDEE
      HFGUAUEUFUG $.
      $( [6-Feb-2015] $)
  $}

