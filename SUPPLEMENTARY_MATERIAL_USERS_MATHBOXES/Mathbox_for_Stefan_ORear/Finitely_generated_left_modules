$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Finitely generated left modules
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LFinGen $.

  $( Extend class notation with the class of finitely generated left
     modules. $)
  clfig $a class LFinGen $.

  ${
    $d w i g $.

    $( Define the class of finitely generated left modules.  Finite generation
       of subspaces can be intepreted using ` |``s ` . $)
    df-lfig $a |- LFinGen = { w e. LMod | ( Base ` w ) e. ( ( LSpan ` w ) "
        ( ~P ( Base ` w ) i^i Fin ) ) } $.
  $}

  ${
    $d ph a b $.  $d W a b $.  $d B a b $.  $d N a b $.
    islmodfg.b $e |- B = ( Base ` W ) $.
    islmodfg.n $e |- N = ( LSpan ` W ) $.
    $( Property of a finitely generated left module.  (Contributed by Stefan
       O'Rear, 1-Jan-2015.) $)
    islmodfg $p |- ( W e. LMod -> ( W e. LFinGen <-> E. b e. ~P B ( b e. Fin /\
          ( N ` b ) = B ) ) ) $=
      ( va clmod wcel clfig cv cbs cfv clspn cpw cfn cin cima wceq wa crab wrex
      wb df-lfig eleq2i fveq2 syl6eqr imaeq1d pweqd ineq1d imaeq2d eqtrd elrab3
      a1i eleq12d wfn wss clss wf eqid lspf ffn syl inss1 fvelimab sylancl elin
      eqcomi pweqi anbi1i bitri eqeq2i anbi12i anass rexbii2 syl6bb 3bitrd ) CH
      IZCJIZCGKZLMZVTNMZWAOZPQZRZIZGHUAZIZCLMZBWIOZPQZRZIZDKZPIZWNBMZASZTZDAOZU
      BZVSWHUCVRJWGCGUDUEUNWFWMGCHVTCSZWAWIWEWLVTCLUFZXAWEBWDRWLXAWBBWDXAWBCNMB
      VTCNUFFUGUHXAWDWKBXAWCWJPXAWAWIXBUIUJUKULUOUMVRWMWPWISZDWKUBZWTVRBWJUPZWK
      WJUQWMXDUCVRWJCURMZBUSXEXFBWICWIUTXFUTFVAWJXFBVBVCWJPVDDWJWKWIBVEVFXCWRDW
      KWSWNWKIZXCTWNWSIZWOTZWQTXHWRTXGXIXCWQXGWNWJIZWOTXIWNWJPVGXJXHWOWJWSWNWIA
      AWIEVHZVIUEVJVKWIAWPXKVLVMXHWOWQVNVKVOVPVQ $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d ph a b $.  $d W a b $.  $d X a b $.  $d S a b $.  $d U a b $.
    $d N a b $.  $d B a $.
    islssfg.x $e |- X = ( W |`s U ) $.
    islssfg.s $e |- S = ( LSubSp ` W ) $.
    islssfg.n $e |- N = ( LSpan ` W ) $.
    $( Property of a finitely generated left (sub-)module.  (Contributed by
       Stefan O'Rear, 1-Jan-2015.) $)
    islssfg $p |- ( ( W e. LMod /\ U e. S ) -> ( X e. LFinGen <->
          E. b e. ~P U ( b e. Fin /\ ( N ` b ) = U ) ) ) $=
      ( clmod wcel wa cfv cbs wceq cpw wrex wb wss eqid cv clspn clfig ressbas2
      cfn lssss pweqd rexeqdv adantl elpwi lsslsp 3expa sylan2 ad2antlr eqeq12d
      syl anbi2d rexbidva lsslmod islmodfg 3bitr4rd ) DJKZBAKZLZFUAZUEKZVEEUBMZ
      MZENMZOZLZFBPZQZVKFVIPZQZVFVECMZBOZLZFVLQEUCKZVCVMVORVBVCVKFVLVNVCBVIVCBD
      NMZSBVIOZABVTDVTTZHUFBVTEDGWBUDUPZUGUHUIVDVRVKFVLVDVEVLKZLZVQVJVFWEVPVHBV
      IWDVDVEBSZVPVHOZVEBUJVBVCWFWGBVEACVGDEGIVGTZHUKULUMVCWAVBWDWCUNUOUQURVDEJ
      KVSVORABDEGHUSVIVGEFVITWHUTUPVA $.
      $( [1-Jan-2015] $)

    islssfg2.b $e |- B = ( Base ` W ) $.
    $( Property of a finitely generated left (sub-)module, with a relaxed
       constraint on the spanning vectors.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    islssfg2 $p |- ( ( W e. LMod /\ U e. S ) -> ( X e. LFinGen <->
          E. b e. ( ~P B i^i Fin ) ( N ` b ) = U ) ) $=
      ( wcel wa cfn cpw wrex wb wi wss elpw clmod clfig cv cfv wceq cin islssfg
      lssss adantl sstr2 mpan9 lspssid adantlr impbida vex 3bitr4g eleq1 anbi2d
      pweq eleq2d bibi1d imbi12d mpbii com12 adantld pm5.32rd elin anbi1i anass
      bitr2i syl6bb rexbidv2 bitrd ) EUALZCBLZMZFUBLGUCZNLZVQDUDZCUEZMZGCOZPVTG
      AOZNUFZPBCDEFGHIJUGVPWAVTGWBWDVPVQWBLZWAMVQWCLZWAMZVQWDLZVTMZVPWAWEWFVPVT
      WEWFQZVRVTVPWJVTVNVSBLZMZVQVSOZLZWFQZRVPWJRWLVQVSSZVQASZWNWFWLWPWQWLVSASZ
      WPWQWKWRVNBVSAEKIUHUIVQVSAUJUKVNWQWPWKVQDAEKJULUMUNVQVSGUOZTVQAWSTUPVTWLV
      PWOWJVTWKVOVNVSCBUQURVTWNWEWFVTWMWBVQVSCUSUTVAVBVCVDVEVFWIWFVRMZVTMWGWHWT
      VTVQWCNVGVHWFVRVTVIVJVKVLVM $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d W a $.  $d N a $.  $d W a $.  $d V a $.  $d X a $.  $d B a $.
    islssfgi.n $e |- N = ( LSpan ` W ) $.
    islssfgi.v $e |- V = ( Base ` W ) $.
    islssfgi.x $e |- X = ( W |`s ( N ` B ) ) $.
    $( Finitely spanned subspaces are finitely generated.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    islssfgi $p |- ( ( W e. LMod /\ B C_ V /\ B e. Fin ) -> X e. LFinGen ) $=
      ( va clmod wcel wss cfn w3a clfig cv cfv wceq cbs eqid cpw cin wrex elpw2
      cvv fvex eqeltri biimpri 3ad2ant2 simp3 elin fveq2 eqeq1d rcla4ev sylancl
      sylanbrc clss wb simp1 lspcl 3adant3 islssfg2 syl2anc mpbird ) DJKZACLZAM
      KZNZEOKZIPZBQZABQZRZICUAZMUBZUCZVHAVOKZVLVLRZVPVHAVNKZVGVQVFVEVSVGVSVFACC
      DSQUEGDSUFUGUDUHUIVEVFVGUJAVNMUKUPVLTVMVRIAVOVJARVKVLVLVJABULUMUNUOVHVEVL
      DUQQZKZVIVPURVEVFVGUSVEVFWAVGVTABCDGVTTZFUTVACVTVLBDEIHWBFGVBVCVD $.
      $( [24-Jan-2015] $)
  $}

  $( Finitely generated left modules are left modules.  (Contributed by Stefan
     O'Rear, 1-Jan-2015.) $)
  fglmod $p |- ( M e. LFinGen -> M e. LMod ) $=
    ( va clfig clmod cv cbs cfv clspn cpw cfn cin cima wcel crab df-lfig ssrab2
    eqsstri sseli ) CDACBEZFGZSHGTIJKLMZBDNDBOUABDPQR $.
    $( [1-Jan-2015] $)

  ${
    $d ph a b $.  $d D a b $.  $d E a b $.  $d F a b $.  $d A a b $.
    $d B a b $.  $d W a b $.  $d P a b $.  $d U a b $.
    lsmfgcl.u $e |- U = ( LSubSp ` W ) $.
    lsmfgcl.p $e |- P = ( LSSum ` W ) $.
    lsmfgcl.d $e |- D = ( W |`s A ) $.
    lsmfgcl.e $e |- E = ( W |`s B ) $.
    lsmfgcl.f $e |- F = ( W |`s ( A P B ) ) $.
    lsmfgcl.w $e |- ( ph -> W e. LMod ) $.
    lsmfgcl.a $e |- ( ph -> A e. U ) $.
    lsmfgcl.b $e |- ( ph -> B e. U ) $.
    lsmfgcl.df $e |- ( ph -> D e. LFinGen ) $.
    lsmfgcl.ef $e |- ( ph -> E e. LFinGen ) $.
    $( The sum of two finitely generated submodules is finitely generated.
       (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    lsmfgcl $p |- ( ph -> F e. LFinGen ) $=
      ( wcel va vb co cress clfig cv clspn cfv wceq cbs cpw cfn wrex clmod eqid
      cin wb islssfg2 syl2anc mpbid adantr cun wss inss1 sseli elpwi syl lsmsp2
      syl3an 3expb oveq2d unss biimpi syl2an adantl inss2 unfi islssfgi syl3anc
      wa eqeltrd anassrs oveq2 eleq1d syl5ibcom rexlimdva mpd oveq1 syl5eqel )
      AHIBCEUCZUDUCZUENAUAUFZIUGUHZUHZBUIZUAIUJUHZUKZULUPZUMZWKUETZADUETZWSRAIU
      NTZBFTXAWSUQOPWPFBWMIDUALJWMUOZWPUOZURUSUTAWOWTUAWRAWLWRTZVTZIWNCEUCZUDUC
      ZUETZWOWTXFUBUFZWMUHZCUIZUBWRUMZXIAXMXEAGUETZXMSAXBCFTXNXMUQOQWPFCWMIGUBM
      JXCXDURUSUTVAXFXLXIUBWRXFXJWRTZVTIWNXKEUCZUDUCZUETZXLXIAXEXOXRAXEXOVTZVTZ
      XQIWLXJVBZWMUHZUDUCZUEXTXPYBIUDAXEXOXPYBUIZAXBXEWLWPVCZXOXJWPVCZYDOXEWLWQ
      TYEWRWQWLWQULVDZVEWLWPVFVGZXOXJWQTYFWRWQXJYGVEXJWPVFVGZEWLXJWMWPIXDXCKVHV
      IVJVKXTXBYAWPVCZYAULTZYCUETAXBXSOVAXSYJAXEYEYFYJXOYHYIYEYFVTYJWLXJWPVLVMV
      NVOXSYKAXEWLULTXJULTYKXOWRULWLWQULVPZVEWRULXJYLVEWLXJVQVNVOYAWMWPIYCXCXDY
      CUOVRVSWAWBXLXQXHUEXLXPXGIUDXKCWNEWCVKWDWEWFWGWOXHWKUEWOXGWJIUDWNBCEWHVKW
      DWEWFWGWI $.
      $( [24-Jan-2015] $)
  $}

