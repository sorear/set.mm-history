$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Finitely generated left modules
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LFinGen $.

  $( Extend class notation with the class of finitely generated left
     modules. $)
  clfig $a class LFinGen $.

  ${
    $d w i g $.

    $( Define the class of finitely generated left modules.  Finite generation
       of subspaces can be intepreted using ` |``s ` . $)
    df-lfig $a |- LFinGen = { w e. LMod | ( Base ` w ) e. ( ( LSpan ` w ) "
        ( ~P ( Base ` w ) i^i Fin ) ) } $.
  $}

  ${
    lsslss.x $e |- X = ( W |`s U ) $.
    lsslss.s $e |- S = ( LSubSp ` W ) $.
    lsslss.t $e |- T = ( LSubSp ` X ) $.
    $( The subspaces of a subspace are the smaller subspaces. $)
    lsslss $p |- ( ( W e. LMod /\ U e. S ) ->
        ( V e. T <-> ( V e. S /\ V C_ U ) ) ) $=
      ( clmod wcel wa cbs cfv wss cress co wb eqid wceq lsslmod islss3 ressbas2
      syl lssss sseq2d anbi1d sstr2 mpan9 biantrurd oveq1i cin cvv simplr simpr
      ssexg syl2anc ressress incom biimpi syl5eq oveq2d adantl ad2antrr 3bitr4d
      df-ss eqtrd eleq1d pm5.32da ancom syl6bb 3bitr2d ) EJKZCAKZLZDBKZDFMNZOZF
      DPQZJKZLZDCOZVTLZDAKZWBLZVOFJKVPWARACEFGHUABDVQFVQSIUBUDVOWBVRVTVOCVQDVOC
      EMNZOZCVQTACWFEJWFSZHUEZCWFFEGWHUCUDUFUGVOWCWBWDLWEVOWBVTWDVOWBLZEDPQZJKZ
      DWFOZWLLZVTWDWJWMWLVOWGWBWMWIDCWFUHUIUJWJVSWKJWJVSECPQZDPQZWKFWODPGUKWJWP
      ECDULZPQZWKWJVNDUMKZWPWRTVMVNWBUNZWJWBVNWSVOWBUOWTDCAUPUQCDEAUMURUQWBWRWK
      TVOWBWQDEPWBWQDCULZDCDUSWBXADTDCVFUTVAVBVCVGVAVHVMWDWNRVNWBADWFEWHHUBVDVE
      VIWBWDVJVKVL $.
      $( [12-Dec-2014] $)
  $}

  ${
    lsslsp.s $e |- S = ( LSubSp ` W ) $.
    lsslsp.x $e |- X = ( W |`s U ) $.
    lsslsp.k $e |- K = ( LSpan ` W ) $.
    lsslsp.l $e |- L = ( LSpan ` X ) $.
    $( Spans in submodules correspond to spans in the containing module. $)
    lsslsp $p |- ( ( W e. LMod /\ U e. S /\ G C_ U ) ->
        ( K ` G ) = ( L ` G ) ) $=
      ( clmod wcel wss cfv wa 3adant3 eqid syl2anc lspssp w3a simp1 cbs lsslmod
      clss simp3 wceq lssss ressbas2 syl sseqtrd lspcl wb lsslss simpld lspssid
      mpbid syl3anc sstrd mpbir2and eqssd ) FLMZBAMZCBNZUAZCDOZCEOZVEVBVGAMZCVG
      NZVFVGNVBVCVDUBZVEVHVGBNZVEVGGUEOZMZVHVKPZVEGLMZCGUCOZNZVMVBVCVOVDABFGIHU
      DQZVECBVPVBVCVDUFZVEBFUCOZNZBVPUGVBVCWAVDABVTFLVTRZHUHQZBVTGFIWBUIUJUKZVL
      CEVPGVPRZVLRZKULSVBVCVMVNUMVDAVLBVGFGIHWFUNQUQUOVEVOVQVIVRWDCEVPGWEKUPSAC
      VGDFHJTURVEVOVFVLMZCVFNZVGVFNVRVEWGVFAMZVFBNZVEVBCVTNZWIVJVECBVTVSWCUSZAC
      DVTFWBHJULSACBDFHJTVBVCWGWIWJPUMVDAVLBVFFGIHWFUNQUTVEVBWKWHVJWLCDVTFWBJUP
      SVLCVFEGWFKTURVA $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d V s p $.  $d ph s p $.  $d N s p $.  $d W s p $.  $d S s p $.
    lspf.v $e |- V = ( Base ` W ) $.
    lspf.s $e |- S = ( LSubSp ` W ) $.
    lspf.n $e |- N = ( LSpan ` W ) $.
    $( The span operator on a left module maps subsets to subsets. $)
    lspf $p |- ( W e. LMod -> N : ~P V --> S ) $=
      ( vs vp clmod wcel cpw wf cv wss crab cint cmpt wa c0 wne ssrab2 a1i wrex
      simpl lss1 elpwi sseq2 rcla4ev syl2an rabn0 sylibr lssintcl syl3anc fmptd
      eqid lspfval feq1d mpbird ) DJKZCLZABMVAAHVAHNZINZOZIAPZQZRZMUTHVAVFAVGUT
      VBVAKZSZUTVEAOZVETUAZVFAKUTVHUEVJVIVDIAUBUCVIVDIAUDZVKUTCAKVBCOZVLVHACDEF
      UFVBCUGVDVMICAVCCVBUHUIUJVDIAUKULVEADFUMUNVGUPUOUTVAABVGIABCDJHEFGUQURUS
      $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d ph a b $.  $d W a b $.  $d B a b $.
    islmodfgOLD.b $e |- B = ( Base ` W ) $.
    $( Property of a finitely generated left module. $)
    islmodfgOLD $p |- ( W e. LMod -> ( W e. LFinGen <-> E. b e. ~P B ( b e. Fin
        /\
          ( ( LSpan ` W ) ` b ) = B ) ) ) $=
      ( va clmod wcel clfig cv cbs cfv clspn cpw cfn cin cima wceq wa wb eqid
      crab wrex df-lfig eleq2i fveq2 imaeq1d pweqd ineq1d imaeq2d eqtrd eleq12d
      a1i elrab3 wfn wss clss wf lspf ffn syl inss1 fvelimab sylancl elin pweqi
      eqcomi anbi1i bitri eqeq2i anbi12i anass rexbii2 bitrd 3bitrd ) BFGZBHGZB
      EIZJKZVQLKZVRMZNOZPZGZEFUAZGZBJKZBLKZWFMZNOZPZGZCIZNGZWLWGKZAQZRZCAMZUBZV
      PWESVOHWDBEUCUDULWCWKEBFVQBQZVRWFWBWJVQBJUEZWSWBWGWAPWJWSVSWGWAVQBLUEUFWS
      WAWIWGWSVTWHNWSVRWFWTUGUHUIUJUKUMVOWKWNWFQZCWIUBZWRVOWGWHUNZWIWHUOWKXBSVO
      WHBUPKZWGUQXCXDWGWFBWFTXDTWGTURWHXDWGUSUTWHNVACWHWIWFWGVBVCXBWRSVOXAWPCWI
      WQWLWIGZXARWLWQGZWMRZWORXFWPRXEXGXAWOXEWLWHGZWMRXGWLWHNVDXHXFWMWHWQWLWFAA
      WFDVFZVEUDVGVHWFAWNXIVIVJXFWMWOVKVHVLULVMVN $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d ph a b $.  $d W a b $.  $d B a b $.  $d N a b $.
    islmodfg.b $e |- B = ( Base ` W ) $.
    islmodfg.n $e |- N = ( LSpan ` W ) $.
    $( Property of a finitely generated left module. $)
    islmodfg $p |- ( W e. LMod -> ( W e. LFinGen <-> E. b e. ~P B ( b e. Fin /\
          ( N ` b ) = B ) ) ) $=
      ( va clmod wcel clfig cv cbs cfv clspn cpw cfn cin cima wceq wa crab wrex
      wb df-lfig eleq2i fveq2 syl6eqr imaeq1d pweqd ineq1d imaeq2d eqtrd elrab3
      a1i eleq12d wfn wss clss wf eqid lspf ffn syl inss1 fvelimab sylancl elin
      eqcomi pweqi anbi1i bitri eqeq2i anbi12i anass rexbii2 syl6bb 3bitrd ) CH
      IZCJIZCGKZLMZVTNMZWAOZPQZRZIZGHUAZIZCLMZBWIOZPQZRZIZDKZPIZWNBMZASZTZDAOZU
      BZVSWHUCVRJWGCGUDUEUNWFWMGCHVTCSZWAWIWEWLVTCLUFZXAWEBWDRWLXAWBBWDXAWBCNMB
      VTCNUFFUGUHXAWDWKBXAWCWJPXAWAWIXBUIUJUKULUOUMVRWMWPWISZDWKUBZWTVRBWJUPZWK
      WJUQWMXDUCVRWJCURMZBUSXEXFBWICWIUTXFUTFVAWJXFBVBVCWJPVDDWJWKWIBVEVFXCWRDW
      KWSWNWKIZXCTWNWSIZWOTZWQTXHWRTXGXIXCWQXGWNWJIZWOTXIWNWJPVGXJXHWOWJWSWNWIA
      AWIEVHZVIUEVJVKWIAWPXKVLVMXHWOWQVNVKVOVPVQ $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d ph a b $.  $d W a b $.  $d X a b $.  $d S a b $.  $d U a b $.
    $d N a b $.
    islssfg.x $e |- X = ( W |`s U ) $.
    islssfg.s $e |- S = ( LSubSp ` W ) $.
    islssfg.n $e |- N = ( LSpan ` W ) $.
    $( Property of a finitely generated left (sub-)module. $)
    islssfg $p |- ( ( W e. LMod /\ U e. S ) -> ( X e. LFinGen <->
          E. b e. ~P U ( b e. Fin /\ ( N ` b ) = U ) ) ) $=
      ( clmod wcel wa cfv cbs wceq cpw wrex wss eqid syl cv clspn clfig rexeqdv
      cfn lssss ressbas2 pweqd elpwi lsslsp 3expa sylan2 adantr anbi2d rexbidva
      eqeq12d wb lsslmod eqid1 islmodfg 3bitr4rd ) DJKZBAKZLZFUAZUEKZVEEUBMZMZE
      NMZOZLZFBPZQVKFVIPZQZVFVECMZBOZLZFVLQEUCKZVDVKFVLVMVDBVIVDBDNMZRBVIOZABVS
      DJVSSZHUFBVSEDGWAUGTZUHUDVDVQVKFVLVDVEVLKZLZVPVJVFWDVOVHBVIWCVDVEBRZVOVHO
      ZVEBUIVBVCWEWFABVECVGDEHGIVGSZUJUKULVDVTWCWBUMUPUNUOVDEJKVRVNUQABDEGHURVI
      VGEFVIUSWGUTTVA $.
      $( [1-Jan-2015] $)
  $}

  $( Finitely generated left modules are left modules. $)
  fglmod $p |- ( M e. LFinGen -> M e. LMod ) $=
    ( va clfig clmod cv cbs cfv clspn cpw cfn cin cima wcel crab df-lfig ssrab2
    eqsstri sseli ) CDACBEZFGZSHGTIJKLMZBDNDBOUABDPQR $.
    $( [1-Jan-2015] $)

