  ${
    $d A a b d $.  $d B a b c d $.
    $( Polynomials are finitary objects and can only reference a finite number
       of variables, even if the index set is infinite.  Thus, every polynomial
       can be expressed as a (uniquely minimal, although we do not prove that)
       polynomial on a finite number of variables, which is then extended by
       adding an arbitrary set of ignored variables.  (Contributed by Stefan
       O'Rear, 9-Oct-2014.) $)
    mzpcompact2 $p |- ( A e. ( mzPoly ` B ) -> E. a e. Fin E. b e. ( mzPoly ` a
        ) ( a C_ B /\ A = ( c e. ( ZZ ^m B ) |-> ( b ` ( c |` a ) ) ) ) ) $=
      ( vd cvv wcel cmzp cfv cv wss cz cmap co cmpt wceq wa wrex cfn cdm elfvdm
      cres dmmzp syl6eleq wi fveq2 eleq2d sseq2 oveq2 mpteq1 syl eqeq2d anbi12d
      2rexbidv imbi12d vex mzpcompact2lem vtoclg mpcom ) BGHABIJZHZCKZBLZAEMBNO
      ZEKVCUCDKJZPZQZRZDVCIJZSCTSZVBBIUAGABIUBUDUEAFKZIJZHZVCVLLZAEMVLNOZVFPZQZ
      RZDVJSCTSZUFVBVKUFFBGVLBQZVNVBVTVKWAVMVAAVLBIUGUHWAVSVICDTVJWAVOVDVRVHVLB
      VCUIWAVQVGAWAVPVEQVQVGQVLBMNUJEVPVEVFUKULUMUNUOUPAVLCDEFUQURUSUT $.
      $( [9-Oct-2014] $)
  $}
$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Miscellanea for Diophantine sets 1
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two real numbers are equal to 0 iff their Euclidean norm is.  Closed
     theorem of ~ sumsqeq0 .  (Contributed by Stefan O'Rear, 5-Oct-2014.) $)
  sumsqeq0 $p |- ( ( A e. RR /\ B e. RR ) -> ( ( A = 0 /\ B = 0 ) <-> ( ( A ^ 2
      ) + ( B ^ 2 ) ) = 0 ) ) $=
    ( cr wcel wa cc0 wceq c2 cexp co caddc wne wo wb neeq1 oveq1 neeq1d bibi12d
    cif 0re wn neorian orbi1d oveq1d orbi2d oveq2d sumsqne0i dedth2h necon4bbid
    elimel syl5bbr ) ACDZBCDZEZAFGBFGEZAHIJZBHIJZKJZFUOUAAFLZBFLZMZUNURFLZAFBFU
    BULUMVAVBNULAFSZFLZUTMZVCHIJZUQKJZFLZNVDUMBFSZFLZMZVFVIHIJZKJZFLZNABFFAVCGZ
    VAVEVBVHVOUSVDUTAVCFOUCVOURVGFVOUPVFUQKAVCHIPUDQRBVIGZVEVKVHVNVPUTVJVDBVIFO
    UEVPVGVMFVPUQVLVFKBVIHIPUFQRVCVIAFCTUJBFCTUJUGUHUKUI $.
    $( [5-Oct-2014] $)

  $( A composition of two relations is empty iff there is no overlap betwen the
     range of the second and the domain of the first.  Useful in combination
     with ~ coundi and ~ coundir to prune meaningless terms in the result.
     (Contributed by Stefan O'Rear, 8-Oct-2014.) $)
  coeq0 $p |- ( ( A o. B ) = (/) <-> ( dom A i^i ran B ) = (/) ) $=
    ( ccom c0 wceq crn cres cdm wrel wb relco relrn0 ax-mp eqeq1i relres reldm0
    cin rnco dmres incom eqtri 3bitr3i 3bitri ) ABCZDEZUDFZDEZABFZGZFZDEZAHZUHQ
    ZDEZUDIUEUGJABKUDLMUFUJDABRNUIDEZUIHZDEZUKUNUIIZUOUQJAUHOZUIPMURUOUKJUSUILM
    UPUMDUPUHULQUMAUHSUHULTUANUBUC $.
    $( [8-Oct-2014] $)

  $( ~ coeq0 but without explicitly introducing domain and range symbols.
     (Contributed by Stefan O'Rear, 16-Oct-2014.) $)
  coeq0i $p |- ( ( A : C --> D /\ B : E --> F /\ ( C i^i F ) = (/) ) ->
      ( A o. B ) = (/) ) $=
    ( wf cin c0 wceq w3a cdm crn ccom wss frn 3ad2ant2 sslin syl fdm ineq1d ss0
    3ad2ant1 simp3 eqtrd sseqtrd coeq0 sylibr ) CDAGZEFBGZCFHZIJZKZALZBMZHZIJZA
    BNIJUMUPIOUQUMUPUNFHZIUMUOFOZUPUROUJUIUSULEFBPQUOFUNRSUMURUKIUMUNCFUIUJUNCJ
    ULCDATUCUAUIUJULUDUEUFUPUBSABUGUH $.
    $( [16-Oct-2014] $)

  $( Split a finite 1-based set of integers in the middle, allowing either end
     to be empty ( ` ( 1 ... 0 ) ` ).  (Contributed by Stefan O'Rear,
     8-Oct-2014.) $)
  fzsplit1nn0 $p |- ( ( A e. NN0 /\ B e. NN0 /\ A <_ B ) -> ( 1 ... B ) = ( ( 1
      ... A ) u. ( ( A + 1 ) ... B ) ) ) $=
    ( cn0 wcel cle wbr c1 cfz co caddc cun wceq cn cc0 wo wa adantr cz syl6eq
    c0 wi elnn0 simprl nnge1 simprr wb nnz nn0z ad2antrl elfz syl3anc mpbir2and
    1z a1i fzsplit syl2anc uncom oveq1 ax-1cn addid2i oveq1d oveq2 fz10 uneq12d
    un0 syl5req jaoian ex sylbi 3impib ) ACDZBCDZABEFZGBHIZGAHIZAGJIZBHIZKZLZVK
    AMDZANLZOZVLVMPZVSUAAUBWBWCVSVTWCVSWAVTWCPZVLAVNDZVSVTVLVMUCWDWEGAEFZVMVTWF
    WCAUDQVTVLVMUEWDARDZGRDZBRDZWEWFVMPUFVTWGWCAUGQWHWDUMUNVLWIVTVMBUHUIAGBUJUK
    ULCAGBUOUPWAWCPZVRVQVOKZVNVOVQUQWJWKVNTKVNWJVQVNVOTWJVPGBHWJVPNGJIZGWAVPWLL
    WCANGJURQGUSUTSVAWJVOGNHIZTWAVOWMLWCANGHVBQVCSVDVNVESVFVGVHVIVJ $.
    $( [8-Oct-2014] $)

