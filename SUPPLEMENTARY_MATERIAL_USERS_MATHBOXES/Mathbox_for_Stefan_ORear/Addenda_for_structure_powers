$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Addenda for structure powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d B x y z w $.  $d F x y z w $.  $d A x y z w $.  $d V x y z w $.

    $( Membership in a class of singleton functions.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    elixpsn $p |- ( A e. V -> ( F e. X_ x e. { A } B <->
        E. y e. B F = { <. A , y >. } ) ) $=
      ( vz vw cv csn cixp wcel cop wceq wrex cvv cfv wa vex eleq1d ixpeq1 opeq1
      sneq syl eleq2d sneqd eqeq2d rexbidv elex snex eleq1 mpbiri rexlimivw wfn
      eqeq1 wral elixp wsb ralsn weq fveq2 sbcie bitri anbi2i wf biimpri adantl
      simpl ffnfv sylanbrc fsn2 sylib opeq2 rcla4ev fvsn id syl5eqel fnsn jctil
      fneq1 fveq1 anbi12d syl5ibrcom rexlimiv impbii 3bitri vtoclbg pm5.21nii )
      EAGIZJZDKZLZEWIBIZMZJZNZBDOZEACJZDKZLECWMMZJZNZBDOGCFWICNZWKWSEXCWJWRNWKW
      SNWICUCAWJWRDUAUDUEXCWPXBBDXCWOXAEXCWNWTWICWMUBUFUGUHWLEPLZWQEWKUIWPXDBDW
      PXDWOPLWNUJEWOPUKULUMHIZWKLZXEWONZBDOZWLWQHEPXEEWKUKXEENXGWPBDXEEWOUOUHXF
      XEWJUNZAIZXEQZDLZAWJUPZRXIWIXEQZDLZRZXHAWJDXEHSUQXMXOXIXMXLAGURXOXLAWIGSZ
      USXLXOAWIXQAGUTXKXNDXJWIXEVATVBVCVDXPXHXPXOXEWIXNMZJZNZRZXHXPWJDXEVEZYAXP
      XIWMXEQZDLZBWJUPZYBXIXOVHXOYEXIYEXOYEYDBGURXOYDBWIXQUSYDXOBWIXQBGUTYCXNDW
      MWIXEVATVBVCVFVGBWJDXEVIVJWIDXEXQVKVLXGXTBXNDWMXNNZWOXSXEYFWNXRWMXNWIVMUF
      UGVNUDXGXPBDWMDLZXPXGWOWJUNZWIWOQZDLZRYGYJYHYGYIWMDWIWMXQBSVOYGVPVQWIWMXQ
      VRVSXGXIYHXOYJWJXEWOVTXGXNYIDWIXEWOWATWBWCWDWEWFWGWHWG $.
      $( [24-Jan-2015] $)
  $}

  ${
    pwselbasb.y $e |- Y = ( R ^s I ) $.
    pwselbasb.b $e |- B = ( Base ` R ) $.
    pwselbasb.c $e |- C = ( Base ` Y ) $.
    $( Membership in the base set of a structure product.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    pwselbasb $p |- ( ( R e. V /\ I e. W ) -> ( X e. C <-> X : I --> B ) ) $=
      ( wcel wa cmap co wf cbs cfv pwsbas cvv syl6eqr eleq2d fvex elmapg adantl
      wb eqeltri mpan bitr3d ) CELZDFLZMZGADNOZLZGBLDAGPZULUMBGULUMHQRBACDEFHIJ
      SKUAUBUKUNUOUFZUJATLUKUPACQRTJCQUCUGADGTFUDUHUEUI $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d I a b c x y $.  $d A a b c x y $.  $d V a b c x y $.  $d F a b c y $.
    $d W a b c x y $.  $d X x c $.
    ixpsnf1o.f $e |- F = ( x e. A |-> ( { I } X. { x } ) ) $.
    $( A bijection between a class and single-point functions to it.
       (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    ixpsnf1o $p |- ( I e. V -> F : A -1-1-onto-> X_ y e. { I } A ) $=
      ( va vb vc wcel csn cv crn cuni cvv wa vex wceq eqeq2d cixp cxp snex xpex
      a1i rnex uniex cop wrex wb sneq xpeq1d anbi2d elixpsn ax-mp bicomi ixpeq1
      syl eleq2d syl5bb anbi1d bibi12d xpsn eqeq2i anbi2i eqid1 weq opeq2 sneqd
      rcla4ev mpan2 op2nda eqcomi jca eqeq1 rexbidv rneq anbi12d syl5ibrcom imp
      unieqd id1 eqidd eleq1 syl5bi imbi12d rexlimiv impbii bitri vtoclg f1od
      wi ) EFKZAHCBELZCUAZWNAMZLZUBZHMZNZOZDPPGWRPKWMWPCKZQWNWQEUCWPUCUDUEXAPKW
      MWSWOKZQWTWSHRUFUGUEXBWSIMZLZWQUBZSZQZWSXDJMZUHZLZSZJCUIZWPXASZQZUJXBWSWR
      SZQZXCXNQZUJIEFXDESZXHXQXOXRXSXGXPXBXSXFWRWSXSXEWNWQXDEUKZULTUMXSXMXCXNXM
      WSBXECUAZKZXSXCYBXMXDPKYBXMUJIRZBJXDCWSPUNUOUPXSYAWOWSXSXEWNSYAWOSXTBXEWN
      CUQURUSUTVAVBXHXBWSXDWPUHZLZSZQZXOXGYFXBXFYEWSXDWPYCARZVCVDVEYGXOXBYFXOXB
      XOYFYEXKSZJCUIZWPYENZOZSZQXBYJYMXBYEYESZYJYEVFYIYNJWPCJAVGZXKYEYEYOXJYDXI
      WPXDVHVITVJVKYMXBYLWPXDWPYCYHVLVMUEVNYFXMYJXNYMYFXLYIJCWSYEXKVOVPYFXAYLWP
      YFWTYKWSYEVQWATVRVSVTXMXNYGXLXNYGWLZJCXICKZYPXLWPXKNZOZSZXBXKYESZQZWLYTAJ
      VGZYQUUBYSXIWPXDXIYCJRVLVDYQUUBUUCYQXKXKSZQYQYQUUDYQWBYQXKWCVNUUCXBYQUUAU
      UDWPXICWDUUCYEXKXKUUCYDXJWPXIXDVHVITVRVSWEXLXNYTYGUUBXLXAYSWPXLWTYRWSXKVQ
      WATXLYFUUAXBWSXKYEVOUMWFVSWGVTWHWIWJWK $.
      $( [24-Jan-2015] $)

    $( A bijection between a set and single-point functions to it.
       (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    mapsnf1o $p |- ( ( A e. V /\ I e. W ) ->
        F : A -1-1-onto-> ( A ^m { I } ) ) $=
      ( vy wcel wa csn cmap co wf1o cixp ixpsnf1o adantl wceq wb cvv eqcomd syl
      snex ixpconstg mpan adantr f1oeq3 mpbird ) BEIZDFIZJZBBDKZLMZCNZBHULBOZCN
      ZUJUPUIAHBCDFGPQUKUMUORZUNUPSUIUQUJULTIZUIUQDUCURUIJUOUMHULBTEUDUAUEUFUMU
      OBCUGUBUH $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d Y x $.  $d R x $.  $d I x $.  $d B x $.  $d C x $.  $d W x $.
    pwssnf1o.y $e |- Y = ( R ^s { I } ) $.
    pwssnf1o.b $e |- B = ( Base ` R ) $.
    pwssnf1o.f $e |- F = ( x e. B |-> ( { I } X. { x } ) ) $.
    pwssnf1o.c $e |- C = ( Base ` Y ) $.
    $( Triviality of singleton powers: set equipollence.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    pwssnf1o $p |- ( ( R e. V /\ I e. W ) -> F : B -1-1-onto-> C ) $=
      ( wcel wa wf1o cvv cbs cfv wceq csn cmap co fvex eqeltri mapsnf1o syl2anc
      a1i simpr wb snex pwsbas mpan2 adantr syl6eqr eqcomd f1oeq3 syl mpbird )
      DGNZFHNZOZBCEPZBBFUAZUBUCZEPZVBBQNZVAVFVGVBBDRSQKDRUDUEUHUTVAUIABEFQHLUFU
      GVBCVETVCVFUJVBVECVBVEIRSZCUTVEVHTZVAUTVDQNVIFUKBDVDGQIJKULUMUNMUOUPCVEBE
      UQURUS $.
      $( [24-Jan-2015] $)
  $}

  ${
    pwsdiagel.y $e |- Y = ( R ^s I ) $.
    pwsdiagel.b $e |- B = ( Base ` R ) $.
    pwsdiagel.c $e |- C = ( Base ` Y ) $.
    $( Membership of diagonal elements in the structure power base set.
       (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    pwsdiagel $p |- ( ( ( R e. V /\ I e. W ) /\ A e. B ) ->
        ( I X. { A } ) e. C ) $=
      ( wcel wa csn cxp wf wss fconstg snssi fss adantl pwselbasb adantr mpbird
      syl2anc wb ) DFLEGLMZABLZMEANZOZCLZEBUJPZUHULUGUHEUIUJPUIBQULEABRABSEUIBU
      JTUEUAUGUKULUFUHBCDEFGUJHIJKUBUCUD $.
      $( [24-Jan-2015] $)
  $}

  ${
    pwssca.y $e |- Y = ( R ^s I ) $.
    pwssca.s $e |- S = ( Scalar ` R ) $.
    $( The ring of scalars of a structure product.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    pwssca $p |- ( ( R e. V /\ I e. W ) -> S = ( Scalar ` Y ) ) $=
      ( wcel wa csn cxp cprdm co csca cfv cvv wceq fvex eqeltri snex xpexg eqid
      mpan2 adantl prdmsca sylancr pwsval fveq2d eqtr4d ) ADIZCEIZJZBBCAKZLZMNZ
      OPZFOPUMBQIUOQIZBUQRBAOPQHAOSTULURUKULUNQIURAUACUNEQUBUDUEUPUOBQQUPUCUFUG
      UMFUPOABCDEFGHUHUIUJ $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d B x $.  $d I x $.  $d V x $.  $d W x $.  $d X x $.
    fdiagfn.f $e |- F = ( x e. B |-> ( I X. { x } ) ) $.
    $( Functionality of the diagonal map.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    fdiagfn $p |- ( ( B e. V /\ I e. W ) -> F : B --> ( B ^m I ) ) $=
      ( wcel wa cv csn cxp cmap co wf wss fconstg snssi fss syl2anc adantl wb
      elmapg adantr mpbird fmptd ) BEHDFHIZABDAJZKZLZBDMNZCUGUHBHZIUJUKHZDBUJOZ
      ULUNUGULDUIUJOUIBPUNDUHBQUHBRDUIBUJSTUAUGUMUNUBULBDUJEFUCUDUEGUF $.
      $( [24-Jan-2015] $)

    $( Functionality of the diagonal map.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    fvdiagfn $p |- ( ( I e. W /\ X e. B ) -> ( F ` X ) = ( I X. { X } ) ) $=
      ( wcel wa csn cxp cvv cfv wceq simpr snex xpexg mpan2 adantr cv syl2anc
      sneq xpeq2d fvmptg ) DEHZFBHZIUFDFJZKZLHZFCMUHNUEUFOUEUIUFUEUGLHUIFPDUGEL
      QRSAFDATZJZKUHBLCUJFNUKUGDUJFUBUCGUDUA $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d Y x a b $.  $d R x a b $.  $d I x a b $.  $d B x a b $.  $d F a b $.
    $d W x a b $.
    pwsdiagghm.y $e |- Y = ( R ^s I ) $.
    pwsdiagghm.b $e |- B = ( Base ` R ) $.
    pwsdiagghm.f $e |- F = ( x e. B |-> ( I X. { x } ) ) $.
    $( Diagonal homomorphism into a structure power.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    pwsdiagghm $p |- ( ( R e. Grp /\ I e. W ) -> F e. ( R GrpHom Y ) ) $=
      ( va vb cgrp wcel wa cbs cfv co wceq cvv wf cplusg wral cghm simpl pwsgrp
      cv jca cmap fvex eqeltri fdiagfn mpan adantl wb pwsbas feq3 syl mpbid csn
      cxp cof eqid simpll simplr pwsdiagel adantrr adantrl pwsplusgval id ofc12
      vex ad2antlr eqtr2d grpcl 3expb adantlr syl2anc oveqan12d anandis adantll
      a1i fvdiagfn 3eqtr4d ralrimivva cpws ovex isghm ax-mp sylanbrc ) CMNZEFNZ
      OZWKGMNZOZBGPQZDUAZKUGZLUGZCUBQZRZDQZWRDQZWSDQZGUBQZRZSZLBUCKBUCZOZDCGUDR
      NZWMWKWNWKWLUECEFGHUFUHWMWQXHWMBBEUIRZDUAZWQWLXLWKBTNWLXLBCPQTICPUJUKABDE
      TFJULUMUNWMXKWPSXLWQUOBCEMFGHIUPXKWPBDUQURUSWMXGKLBBWMWRBNZWSBNZOZOZEXAUT
      VAZEWRUTVAZEWSUTVAZXERZXBXFXPXTXRXSWTVBRZXQXPWTWPXECXRXSEMFGHWPVCZWKWLXOV
      DWKWLXOVEZWMXMXRWPNXNWRBWPCEMFGHIYBVFVGWMXNXSWPNXMWSBWPCEMFGHIYBVFVHWTVCZ
      XEVCZVIWLYAXQSWKXOWLEWRWSWTFTTWLVJWRTNWLKVLWBWSTNWLLVLWBVKVMVNXPWLXABNZXB
      XQSYCWKXOYFWLWKXMXNYFBWTCWRWSIYDVOVPVQABDEFXAJWCVRWLXOXFXTSZWKWLXMXNYGWLX
      MOWLXNOXCXRXDXSXEABDEFWRJWCABDEFWSJWCVSVTWAWDWEUHGTNXJWOXIOUOGCEWFRTHCEWF
      WGUKLKWTXECGDTBWPIYBYDYEWHWIWJ $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d Y x a b $.  $d R x a b $.  $d I x a b $.  $d B x a b $.  $d F a b $.
    $d W x a b $.
    pwsdiaglmhm.y $e |- Y = ( R ^s I ) $.
    pwsdiaglmhm.b $e |- B = ( Base ` R ) $.
    pwsdiaglmhm.f $e |- F = ( x e. B |-> ( I X. { x } ) ) $.
    $( Diagonal homomorphism into a structure power.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    pwsdiaglmhm $p |- ( ( R e. LMod /\ I e. W ) -> F e. ( R LMHom Y ) ) $=
      ( va vb clmod wcel wa co cfv wceq eqid cvv cghm csca cvsca wral cbs clmhm
      w3a simpl pwslmod jca cgrp lmodgrp pwsdiagghm sylan pwssca eqcomd csn cxp
      cv cof simpll simplr simprl pwsdiagel adantrl pwsvscafval id vex ad2antlr
      a1i ofc12 eqtr2d lmodvscl 3expb adantlr fvdiagfn syl2anc ad2ant2l 3eqtr4d
      oveq2d ralrimivva 3jca wb cpws ovex eqeltri islmhm ax-mp sylanbrc ) CMNZE
      FNZOZWJGMNZOZDCGUAPNZGUBQZCUBQZRZKUSZLUSZCUCQZPZDQZWSWTDQZGUCQZPZRZLBUDKW
      QUEQZUDZUGZDCGUFPNZWLWJWMWJWKUHCEFGHUIUJWLWOWRXIWJCUKNWKWOCULABCDEFGHIJUM
      UNWLWQWPCWQEMFGHWQSZUOUPWLXGKLXHBWLWSXHNZWTBNZOZOZEXBUQURZWSEWTUQURZXEPZX
      CXFXPXSEWSUQURXRXAUTPZXQXPWSGUEQZCXAXEWQEXHMFXRGHYASZXASZXESZXLXHSZWJWKXO
      VAWJWKXOVBZWLXMXNVCWLXNXRYANXMWTBYACEMFGHIYBVDVEVFWKXTXQRWJXOWKEWSWTXAFTT
      WKVGWSTNWKKVHVJWTTNWKLVHVJVKVIVLXPWKXBBNZXCXQRYFWJXOYGWKWJXMXNYGWSXAWQXHB
      CWTIXLYCYEVMVNVOABDEFXBJVPVQXPXDXRWSXEWKXNXDXRRWJXMABDEFWTJVPVRVTVSWAWBGT
      NXKWNXJOWCGCEWDPTHCEWDWEWFKLXHCGBDWQWPXAXETXLWPSYEIYCYDWGWHWI $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d B x $.  $d V x $.  $d X x $.
    fvtresfn.f $e |- F = ( x e. B |-> ( x |` V ) ) $.
    $( Functionality of a tuple-restriction function.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    fvtresfn $p |- ( X e. B -> ( F ` X ) = ( X |` V ) ) $=
      ( wcel cres cvv cfv wceq resexg cv reseq1 fvmptg mpdan ) EBGEDHZIGECJQKED
      BLAEAMZDHQBICREDNFOP $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d A y $.  $d B y $.  $d C x y $.
    $( Unrestricted restriction of the mapping operation.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    resmpt3 $p |- ( ( x e. A |-> B ) |` C ) = ( x e. ( A i^i C ) |-> B ) $=
      ( vy cv wcel wceq wa copab cres cin cmpt resopab ancom bitri anbi1i anass
      elin df-mpt bitr2i opabbii eqtri reseq1i 3eqtr4i ) AFZBGZEFCHZIZAEJZDKZUF
      BDLZGZUHIZAEJZABCMZDKAULCMUKUFDGZUIIZAEJUOUIAEDNURUNAEUNUQUGIZUHIURUMUSUH
      UMUGUQIUSUFBDSUGUQOPQUQUGUHRUAUBUCUPUJDAEBCTUDAEULCTUE $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d F x a b $.  $d G x a b $.  $d V x $.  $d W x $.  $d R x a b $.
    $d D x $.
    $( General value of ` ( F oF R G ) ` with no assumptions on functionality
       of ` F ` and ` G ` .  (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    offval3 $p |- ( ( F e. V /\ G e. W ) -> ( F oF R G ) =
        ( x e. ( dom F i^i dom G ) |-> ( ( F ` x ) R ( G ` x ) ) ) ) $=
      ( va vb wcel wa cvv cdm cin cv cfv co cmpt wceq elex adantr adantl inex1g
      dmexg mptexg 3syl dmeq ineqan12d fveq1 oveqan12d mpteq12dv df-of ovmpt2ga
      cof syl3anc ) CEIZDFIZJCKIZDKIZACLZDLZMZANZCOZVBDOZBPZQZKIZCDBUMZPVFRUOUQ
      UPCESTUPURUODFSUAUOVGUPUOUSKIVAKIVGCEUCUSUTKUBAVAVEKUDUETGHCDKKAGNZLZHNZL
      ZMZVBVIOZVBVKOZBPZQVFVHKVICRZVKDRZJAVMVPVAVEVQVRVJUSVLUTVICUFVKDUFUGVQVRV
      NVCVOVDBVBVICUHVBVKDUHUIUJABGHUKULUN $.
      $( [24-Jan-2015] $)

    $( Pointwise combination commutes with restriction.  (Contributed by Stefan
       O'Rear, 24-Jan-2015.) $)
    offres $p |- ( ( F e. V /\ G e. W ) -> ( ( F oF R G ) |` D ) =
        ( ( F |` D ) oF R ( G |` D ) ) ) $=
      ( vx wcel cdm cin cfv co cmpt cres wceq fvres dmres 3eqtr4ri offval3 cvv
      wa cv cof inss2 sseli oveq12d mpteq2ia inindi incom ineq12i eqid mpteq12i
      syl resmpt3 reseq1d resexg syl2an 3eqtr4a ) CEHZDFHZUAZGCIZDIZJZGUBZCKZVE
      DKZBLZMZANZGCANZIZDANZIZJZVEVKKZVEVMKZBLZMZCDBUCZLZANVKVMVTLZGVDAJZVRMGWC
      VHMVSVJGWCVRVHVEWCHVEAHZVRVHOWCAVEVDAUDUEWDVPVFVQVGBVEACPVEADPUFUMUGGVOVR
      WCVRAVDJAVBJZAVCJZJWCVOAVBVCUHVDAUIVLWEVNWFCAQDAQUJRVRUKULGVDVHAUNRVAWAVI
      AGBCDEFSUOUSVKTHVMTHWBVSOUTCAEUPDAFUPGBVKVMTTSUQUR $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d F a $.  $d D a $.  $d Y a $.  $d X a x $.  $d V x $.
    mptcnvima.f $e |- F = ( x e. D |-> Y ) $.
    $( Converse image of a function defined by maps-to.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    mptcnvima $p |- ( `' F " X ) = { x e. D | Y e. X } $=
      ( va ccnv cima cv wcel wceq wa copab cres crn eqtri wex cab 3eqtri df-mpt
      crab cnveqi cnvopab imaeq1i df-ima resopab rneqi rnopab ancom anass bitri
      cmpt exbii 19.42v df-clel bicomi anbi2i 3bitri abbii df-rab eqtr4i ) CHZD
      IAJBKZGJZELZMZGANZDIVHDOZPZEDKZABUBZVCVHDVCVGAGNZHVHCVMCABEUMVMFAGBEUAQUC
      VGAGUDQUEVHDUFVJVEDKZVGMZGANZPVOGRZASZVLVIVPVGGADUGUHVOGAUIVRVDVKMZASVLVQ
      VSAVQVDVFVNMZMZGRVDVTGRZMVSVOWAGVOVGVNMWAVNVGUJVDVFVNUKULUNVDVTGUOWBVKVDV
      KWBGEDUPUQURUSUTVKABVAVBTT $.
      $( [25-Jan-2015] $)

    $( Converse singleton image of a function defined by maps-to.  (Contributed
       by Stefan O'Rear, 25-Jan-2015.) $)
    mptiniseg $p |- ( X e. V -> ( `' F " { X } ) = { x e. D | Y = X } ) $=
      ( wcel ccnv csn cima crab wceq mptcnvima elsnc2g rabbidv syl5eq ) EDHZCIE
      JZKFSHZABLFEMZABLABCSFGNRTUAABFEDOPQ $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d Y a b x $.  $d W a b x $.  $d U a b x $.  $d Z a b x $.  $d V a b x $.
    $d B a b x $.  $d C a b x $.  $d F a b $.  $d X a b x $.  $d T x $.
    pwssplit1.y $e |- Y = ( W ^s U ) $.
    pwssplit1.z $e |- Z = ( W ^s V ) $.
    pwssplit1.b $e |- B = ( Base ` Y ) $.
    pwssplit1.c $e |- C = ( Base ` Z ) $.
    pwssplit1.f $e |- F = ( x e. B |-> ( x |` V ) ) $.
    $( Splitting for structure powers, part 0: restriction is a function.
       (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    pwssplit0 $p |- ( ( W e. T /\ U e. X /\ V C_ U ) -> F : B --> C ) $=
      ( wcel wf wb syl2anc wss w3a cv cres wa cbs eqid pwselbasb 3adant3 biimpa
      cfv simpl3 fssres cvv simp1 simp3 simp2 ssexg adantr mpbird fmptd ) HDQZE
      IQZGEUAZUBZABAUCZGUDZCFVEVFBQZUEZVGCQZGHUFUKZVGRZVIEVKVFRZVDVLVEVHVMVBVCV
      HVMSVDVKBHEDIVFJLVKUGZNUHUIUJVBVCVDVHULEVKGVFUMTVEVJVLSZVHVEVBGUNQZVOVBVC
      VDUOVEVDVCVPVBVCVDUPVBVCVDUQGEIURTVKCHGDUNVGKMVNOUHTUSUTPVA $.
      $( [24-Jan-2015] $)

    $( Splitting for structure powers, part 1: restriction is an onto
       function.  The only actual monoid law we need here is that the base set
       is nonempty.  (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    pwssplit1 $p |- ( ( W e. Mnd /\ U e. X /\ V C_ U ) -> F : B -onto-> C ) $=
      ( wcel wf wceq cun c0 va vb cmnd wss w3a cfv wrex wral wfo pwssplit0 cdif
      cv wa c0g csn cxp cbs cin cvv wb simp1 simp3 simp2 syl2anc eqid pwselbasb
      ssexg biimpa fconst a1i simpl1 mndidcl syl snssd fss disjdif fun syl21anc
      fvex simpl3 undif sylib unidm feq23d simpl2 mpbird cres fvtresfn resundir
      mpbid wfn ffn fnresdm incom eqtri fnconstg ax-mp fnresdisj uneq12d syl5eq
      3syl mp1i un0 syl6eq eqtr2d fveq2 eqeq2d rcla4ev ralrimiva dffo3 sylanbrc
      ) GUCPZDHPZFDUDZUEZBCEQUAULZUBULZEUFZRZUBBUGZUACUHBCEUIABCUCDEFGHIJKLMNOU
      JXOXTUACXOXPCPZUMZXPDFUKZGUNUFZUOZUPZSZBPZXPYGEUFZRZXTYBYHDGUQUFZYGQZYBFY
      CSZYKYKSZYGQZYLYBFYKXPQZYCYKYFQZFYCURZTRZYOXOYAYPXOXLFUSPZYAYPUTXLXMXNVAX
      OXNXMYTXLXMXNVBXLXMXNVCFDHVGVDYKCGFUCUSXPJLYKVEZNVFVDVHZYBYCYEYFQZYEYKUDY
      QUUCYBYCYDGUNVSZVIVJYBYDYKYBXLYDYKPXLXMXNYAVKZYKGYDUUAYDVEVLVMVNYCYEYKYFV
      OVDYSYBFDVPZVJFYCYKYKXPYFVQVRYBYMYNDYKYGYBXNYMDRXLXMXNYAVTFDWAWBYNYKRYBYK
      WCVJWDWJYBXLXMYHYLUTUUEXLXMXNYAWEYKBGDUCHYGIKUUAMVFVDWFZYBYIYGFWGZXPYBYHY
      IUUHRUUGABEFYGOWHVMYBUUHXPTSZXPYBUUHXPFWGZYFFWGZSUUIXPYFFWIYBUUJXPUUKTYBY
      PXPFWKUUJXPRUUBFYKXPWLFXPWMXAYBYCFURZTRZUUKTRZUUMYBUULYRTYCFWNUUFWOVJYFYC
      WKZUUMUUNUTYBYDUSPUUOUUDYCYDUSWPWQYCFYFWRXBWJWSWTXPXCXDXEXSYJUBYGBXQYGRXR
      YIXPXQYGEXFXGXHVDXIUBUABCEXJXK $.
      $( [24-Jan-2015] $)

    $( Splitting for structure powers, part 2: restriction is a group
       homomorphism.  (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    pwssplit2 $p |- ( ( W e. Grp /\ U e. X /\ V C_ U ) ->
        F e. ( Y GrpHom Z ) ) $=
      ( cgrp wcel cfv co cvv va vb wss w3a wa wf cv cplusg wceq wral cghm simp1
      simp2 pwsgrp syl2anc simp3 jca pwssplit0 cres offres adantl adantr simpl2
      ssexg cof simprl simprr eqid pwsplusgval reseq1d fvtresfn oveqan12d grpcl
      3eqtr4d 3expb syl ffvelrn adantrr adantrl ralrimivva wb cpws ovex eqeltri
      sylan isghm ax-mp sylanbrc ) GPQZDHQZFDUCZUDZIPQZJPQZUEZBCEUFZUAUGZUBUGZI
      UHRZSZERZWQERZWRERZJUHRZSZUIZUBBUJUABUJZUEZEIJUKSQZWLWMWNWLWIWJWMWIWJWKUL
      ZWIWJWKUMZGDHIKUNUOZWLWIFTQZWNXJWLWKWJXMWIWJWKUPXKFDHVDUOZGFTJLUNUOUQWLWP
      XGABCPDEFGHIJKLMNOURZWLXFUAUBBBWLWQBQZWRBQZUEZUEZWTFUSZXBXCGUHRZVEZSZXAXE
      XSWQWRYBSZFUSZWQFUSZWRFUSZYBSZXTYCXRYEYHUIWLFYAWQWRBBUTVAXSWTYDFXSYABWSGW
      QWRDPHIKMWLWIXRXJVBZWIWJWKXRVCWLXPXQVFWLXPXQVGYAVHZWSVHZVIVJXRYCYHUIWLXPX
      QXBYFXCYGYBABEFWQOVKABEFWROVKVLVAVNXSWTBQZXAXTUIWLWMXRYLXLWMXPXQYLBWSIWQW
      RMYKVMVOWEABEFWTOVKVPXSYACXDGXBXCFPTJLNYIWLXMXRXNVBWLXPXBCQZXQWLWPXPYMXOB
      CWQEVQWEVRWLXQXCCQZXPWLWPXQYNXOBCWREVQWEVSYJXDVHZVIVNVTUQJTQXIWOXHUEWAJGF
      WBSTLGFWBWCWDUBUAWSXDIJETBCMNYKYOWFWGWH $.
      $( [24-Jan-2015] $)

    $( Splitting for structure powers, part 3: restriction is a module
       homomorphism.  (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    pwssplit3 $p |- ( ( W e. LMod /\ U e. X /\ V C_ U ) ->
        F e. ( Y LMHom Z ) ) $=
      ( clmod wcel co cfv cvv va vb wss w3a wa cghm csca wceq cv cvsca wral cbs
      clmhm simp1 simp2 pwslmod syl2anc simp3 ssexg cgrp lmodgrp pwssplit2 eqid
      jca syl3an1 pwssca eqtr3d cres csn cxp cof snex sylancl vex offres adantr
      xpexg xpssres 3ad2ant3 oveq1d simpl1 simpl2 fveq2d eleq2d biimpar adantrr
      eqtrd simprr pwsvscafval reseq1d fvtresfn ad2antll 3eqtr4d lmodvscl 3expb
      oveq2d sylan wf pwssplit0 ffvelrn adantrl ralrimivva 3jca wb cpws eqeltri
      syl ovex islmhm ax-mp sylanbrc ) GPQZDHQZFDUCZUDZIPQZJPQZUEZEIJUFRQZJUGSZ
      IUGSZUHZUAUIZUBUIZIUJSZRZESZYCYDESZJUJSZRZUHZUBBUKUAYAULSZUKZUDZEIJUMRQZX
      OXPXQXOXLXMXPXLXMXNUNZXLXMXNUOZGDHIKUPUQZXOXLFTQZXQYPXOXNXMYSXLXMXNURYQFD
      HUSUQZGFTJLUPUQVDXOXSYBYMXLGUTQXMXNXSGVAABCDEFGHIJKLMNOVBVEXOGUGSZXTYAXOX
      LYSUUAXTUHYPYTGUUAFPTJLUUAVCZVFUQXOXLXMUUAYAUHYPYQGUUADPHIKUUBVFUQZVGXOYK
      UAUBYLBXOYCYLQZYDBQZUEZUEZYFFVHZFYCVIZVJZYHGUJSZVKZRZYGYJUUGDUUIVJZYDUULR
      ZFVHZUUJYDFVHZUULRZUUHUUMUUGUUPUUNFVHZUUQUULRZUURXOUUPUUTUHZUUFXOUUNTQZYD
      TQUVAXOXMUUITQUVBYQYCVLDUUIHTVQVMUBVNFUUKUUNYDTTVOVMVPUUGUUSUUJUUQUULXOUU
      SUUJUHZUUFXNXLUVCXMDUUIFVRVSVPVTWGUUGYFUUOFUUGYCBGUUKYEUUADUUAULSZPHYDIKM
      UUKVCZYEVCZUUBUVDVCZXLXMXNUUFWAZXLXMXNUUFWBXOUUDYCUVDQZUUEXOUVIUUDXOUVDYL
      YCXOUUAYAULUUCWCWDWEWFZXOUUDUUEWHWIWJUUGYHUUQUUJUULUUEYHUUQUHXOUUDABEFYDO
      WKWLWPWMUUGYFBQZYGUUHUHXOXPUUFUVKYRXPUUDUUEUVKYCYEYAYLBIYDMYAVCZUVFYLVCZW
      NWOWQABEFYFOWKXGUUGYCCGUUKYIUUAFUVDPTYHJLNUVEYIVCZUUBUVGUVHXOYSUUFYTVPUVJ
      XOUUEYHCQZUUDXOBCEWRUUEUVOABCPDEFGHIJKLMNOWSBCYDEWTWQXAWIWMXBXCJTQYOXRYNU
      EXDJGFXERTLGFXEXHXFUAUBYLIJBEYAXTYEYITUVLXTVCUVMMUVFUVNXIXJXK $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.  $d C x y $.  $d D a x y $.  $d E x y $.
    $d F a $.  $d G x y $.  $d K a x $.  $d L a x $.  $d R a x y $.
    $d V a x y $.  $d Z x y $.
    pwssplit4.e $e |- E = ( R ^s ( A u. B ) ) $.
    pwssplit4.g $e |- G = ( Base ` E ) $.
    pwssplit4.z $e |- Z = ( 0g ` R ) $.
    pwssplit4.k $e |- K = { y e. G | ( y |` A ) = ( A X. { Z } ) } $.
    pwssplit4.f $e |- F = ( x e. K |-> ( x |` B ) ) $.
    pwssplit4.c $e |- C = ( R ^s A ) $.
    pwssplit4.d $e |- D = ( R ^s B ) $.
    pwssplit4.l $e |- L = ( E |`s K ) $.
    $( Splitting for structure powers 4: maps isomorphically onto the other
       half.  (Contributed by Stefan O'Rear, 25-Jan-2015.) $)
    pwssplit4 $p |- ( ( R e. LMod /\ ( A u. B ) e. V /\ ( A i^i B ) = (/) ) ->
          F e. ( L LMIso D ) ) $=
      ( va clmod wcel cun cin c0 wceq w3a clmhm co cbs cfv wf1o clmim cres cmpt
      cv wss csn cxp crab ssrab2 eqsstri resmpt ax-mp eqtr4i cvv clss ssun2 a1i
      eqid pwssplit3 syld3an3 cpws ovex eqeltri c0g cmnd cgrp simp1 grpmnd 3syl
      lmodgrp ssun1 ssexg mpan 3ad2ant2 syl2anc eqeq2d rabbidv syl5eq ccnv cima
      fvex mptiniseg eqcomi lmhmkerlss sylancl eqeltrd reslmhm syl3anc syl5eqel
      pws0g wf1 crn wi wral fvtresfn eqcomd eqeqan12rd weq reseq1 eqeq1d elrab2
      wa uneq12 resundi xpundir 3eqtr4g wf wfn adantr ffn fnresdm 3adant3 eqtrd
      syl mpbird lmhmf pwselbasb uncom fnresdisj mp2b uneq12d resundir sylanbrc
      wb un0 adantll adantl simpl1 simp2 simprll pwselbas csubg pwslmod lsssubg
      subg0 3eqtr3d exp32 syl5bi imp sylbid ralrimiva cghm lmghm ressbas2 ghmf1
      frn biimpa fconst mndidcl snssd fss incom simp3 fun syl21anc unidm feq23i
      sylib simpl3 mpbid fnconstg eqtr2i resexg fvmptg biimpi 3ad2ant3 fnfvelrn
      syl6eq eqeltrrd ex ssrdv eqssd dff1o5 islmim ) GUDUEZCDUFZMUEZCDUGZUHUIZU
      JZILFUKULZUEZKFUMUNZIUOZILFUPULUEZUWOIAJAUSZDUQZURZKUQZUWPIAKUXBURZUXDSKJ
      UTZUXDUXEUIKBUSZCUQZCNVAZVBZUIZBJVCZJRUXKBJVDVEZAJKUXBVFVGVHUWOUXCHFUKULU
      EZFVIUEZKHVJUNZUEZUXDUWPUEUWJUWLUWNDUWKUTZUXNUXRUWODCVKZVLAJUWRUWKUXCDGMH
      FOUAPUWRVMZUXCVMVNVOUXOUWOFGDVPULVIUAGDVPVQVRZVLUWOKUXHEVSUNZUIZBJVCZUXPU
      WOKUXLUYDRUWOUXKUYCBJUWOUXJUYBUXHUWOGVTUEZCVIUEZUXJUYBUIUWOUWJGWAUEUYEUWJ
      UWLUWNWBZGWEGWCWDZUWLUWJUYFUWNCUWKUTZUWLUYFCDWFZCUWKMWGWHWIGCVIENTQXEWJWK
      WLWMUWOBJUXHURZHEUKULUEZEVIUEUYDUXPUEUWJUWLUWNUYIUYLUYIUWOUYJVLBJEUMUNZUW
      KUYKCGMHEOTPUYMVMUYKVMZVNVOEGCVPULVITGCVPVQVRHEUXPUYKUYDVIUYBUYKWNUYBVAWO
      ZUYDUYBVIUEUYOUYDUIEVSWPBJUYKVIUYBUXHUYNWQVGWRUYBVMUXPVMZWSWTXAZLHFUXPUXC
      VIKUYPUBXBXCXDZUWOKUWRIXFZIXGZUWRUIUWSUWOUYSUCUSZIUNZFVSUNZUIZVUALVSUNZUI
      ZXHZUCKXIZUWOVUGUCKUWOVUAKUEZXQVUDVUADUQZDUXIVBZUIZVUFVUIUWOVUBVUJVUCVUKA
      KIDVUASXJUWOVUKVUCUWOUYEDVIUEZVUKVUCUIUYHUWLUWJVUMUWNUXRUWLVUMUXSDUWKMWGW
      HWIZGDVIFNUAQXEWJXKXLUWOVUIVULVUFXHZVUIVUAJUEZVUACUQZUXJUIZXQZUWOVUOUXKVU
      RBVUAJKBUCXMUXHVUQUXJUXGVUACXNXORXPUWOVUSVULVUFUWOVUSVULXQZXQZVUAUWKUQZUW
      KUXIVBZVUAVUEVUTVVBVVCUIZUWOVURVULVVDVUPVURVULXQVUQVUJUFUXJVUKUFVVBVVCVUQ
      UXJVUJVUKXRVUACDXSCDUXIXTYAUUAUUBVVAUWKGUMUNZVUAYBVUAUWKYCVVBVUAUIVVAVVEG
      UWKJUDVUAHMOVVEVMZPUWJUWLUWNVUTUUCUWOUWLVUTUWJUWLUWNUUDZYDUWOVUPVURVULUUE
      UUFUWKVVEVUAYEUWKVUAYFWDUWOVVCVUEUIVUTUWOVVCHVSUNZVUEUWOUYEUWLVVCVVHUIUYH
      VVGGUWKMHNOQXEWJUWOKHUUGUNUEZVVHVUEUIUWOHUDUEZUXQVVIUWJUWLVVJUWNGUWKMHOUU
      HYGUYQUXPKHUYPUUIWJKHLVVHUBVVHVMUUJYIYHYDUUKUULUUMUUNUUOUUPUWOILFUUQULUEZ
      UXOUYSVUHYSUWOUWQVVKUYRLFIUURYIUYAUCLFVUCIVIKUWRVUEUXFKLUMUNZUIUXMKJLHUBP
      UUSVGZUXTVUEVMVUCVMUUTWTYJUWOUYTUWRUWOVVLUWRIYBZUYTUWRUTUWOUWQUXOVVNUYRUY
      AVVLUWRLFIVIVVLVMUXTYKWTVVLUWRIUVAYIUWOUCUWRUYTUWOVUAUWRUEZVUAUYTUEUWOVVO
      XQZVUAUXJUFZIUNZVUAUYTVVPVVRVVQDUQZVUAVVPVVQKUEZVVSVIUEZVVRVVSUIVVPVVQJUE
      ZVVQCUQZUXJUIZVVTVVPVWBUWKVVEVVQYBZVVPDCUFZVVEVVEUFZVVQYBZVWEVVPDVVEVUAYB
      ZCVVEUXJYBZDCUGZUHUIZVWHUWOVVOVWIUWOUWJVUMVVOVWIYSUYGVUNVVEUWRGDUDVIVUAFU
      AVVFUXTYLWJUVBZVVPCUXIUXJYBZUXIVVEUTVWJVWNVVPCNNGVSUNVIQGVSWPVRZUVCZVLVVP
      NVVEVVPUYENVVEUEUWOUYEVVOUYHYDVVEGNVVFQUVDYIUVECUXIVVEUXJUVFWJUWOVWLVVOUW
      OVWKUWMUHDCUVGZUWJUWLUWNUVHWMYDDCVVEVVEVUAUXJUVIUVJVWFVWGUWKVVEVVQDCYMVVE
      UVKUVLUVMUWOVWBVWEYSZVVOUWJUWLVWRUWNVVEJGUWKUDMVVQHOVVFPYLYGYDYJVVPVUQUXJ
      CUQZUFUHUXJUFZVWCUXJVVPVUQUHVWSUXJVVPVWLVUQUHUIZVVPVWKUWMUHVWQUWJUWLUWNVV
      OUVNWMVVPVWIVUADYCZVWLVXAYSVWMDVVEVUAYEZDCVUAYNWDUVOVWSUXJUIZVVPNVIUEUXJC
      YCZVXDVWOCNVIUVPCUXJYFYOVLYPVUAUXJCYQVWTUXJUHUFUXJUHUXJYMUXJYTUVQYAUXKVWD
      BVVQJKUXGVVQUIUXHVWCUXJUXGVVQCXNXORXPYRZVVPVVTVWAVXFVVQDKUVRYIAVVQUXBVVSK
      VIIUXAVVQDXNSUVSWJVVPVVSVUJUXJDUQZUFZVUAVUAUXJDYQVVPVXHVUAUHUFVUAVVPVUJVU
      AVXGUHVVPVWIVXBVUJVUAUIVWMVXCDVUAYFWDUWOVXGUHUIZVVOUWNUWJVXIUWLUWNVXIVWNV
      XEUWNVXIYSVWPCUXIUXJYECDUXJYNYOUVTUWAYDYPVUAYTUWCWMYHVVPIKYCZVVTVVRUYTUEU
      WOVXJVVOUWOKUWRIYBZVXJUWOUWQUXOVXKUYRUYAKUWRLFIVIVVMUXTYKWTKUWRIYEYIYDVXF
      KVVQIUWBWJUWDUWEUWFUWGKUWRIUWHYRUXOUWTUWQUWSXQYSUYAKUWRLFIVIVVMUXTUWIVGYR
      $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d B a b $.  $d W a b $.
    filnm.b $e |- B = ( Base ` W ) $.
    $( Finite left modules are Noetherian.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    filnm $p |- ( ( W e. LMod /\ B e. Fin ) -> W e. LNoeM ) $=
      ( va vb clmod wcel cfn wa cv clspn cfv wceq cpw cin wrex syl2anc sylanbrc
      clss eqid wral clnm simpl lssss adantl elpw sylibr simplr ssfi elin lspid
      wss vex adantlr eqcomd weq fveq2 eqeq2d rcla4ev ralrimiva islnm2 ) BFGZAH
      GZIZVBDJZEJZBKLZLZMZEANZHOZPZDBSLZUABUBGVBVCUCVDVLDVMVDVEVMGZIZVEVKGZVEVE
      VGLZMZVLVOVEVJGZVEHGZVPVOVEAULZVSVNWAVDVMVEABCVMTZUDUEZVEADUMUFUGVOVCWAVT
      VBVCVNUHWCAVEUIQVEVJHUJRVOVQVEVBVNVQVEMVCVMVEVGBWBVGTZUKUNUOVIVREVEVKEDUP
      VHVQVEVFVEVGUQURUSQUTAVMEDBVGCWBWDVAR $.
      $( [24-Jan-2015] $)
  $}

  ${
    pwslnmlem0.y $e |- Y = ( W ^s (/) ) $.
    $( Zeroeth powers are Noetherian. $)
    pwslnmlem0 $p |- ( W e. LMod -> Y e. LNoeM ) $=
      ( clmod wcel cbs cfv cfn clnm cvv 0ex pwslmod mpan2 cmap wceq eqid pwsbas
      c0 co csn c1o fvex map0e df1o2 eqtri snfi eqeltri syl6eqelr filnm syl2anc
      ) ADEZBDEZBFGZHEBIEUKRJEZULKARJBCLMUKUMAFGZRNSZHUKUNUPUMOKUOARDJBCUOPQMUP
      RTZHUPUAUQUOAFUBUCUDUERUFUGUHUMBUMPUIUJ $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d Y x $.  $d W i x $.
    pwslnmlem1.y $e |- Y = ( W ^s { i } ) $.
    $( First powers are Noetherian. $)
    pwslnmlem1 $p |- ( W e. LNoeM -> Y e. LNoeM ) $=
      ( vx clnm wcel cbs cfv cv csn cxp cmpt clmhm co cvv crn wceq eqid cpws id
      clmod lnmlmod snex pwsdiaglmhm sylancl ovex eqeltri a1i wf1o wfo pwssnf1o
      vex eqid1 mpan2 f1ofo forn 3syl lnmepi syl22anc ) BFGZEBHIZAJZKZEJKLMZBCN
      OGZCPGZVAVEQCHIZRZCFGVABUBGVDPGVFBUCVCUDEVBBVEVDPCDVBSVESZUEUFVGVACBVDTOP
      DBVDTUGUHUIVAUAVAVBVHVEUJZVBVHVEUKVIVAVCPGVKAUMEVBVHBVEVCFPCDVBUNVJVHSZUL
      UOVBVHVEUPVBVHVEUQURVHBCVEPVLUSUT $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d X x y $.  $d A x y $.  $d W x y $.  $d Z x y $.  $d B x y $.
    $d Y x y $.  $d ph x y $.
    pwslnmlem2.a $e |- A e. _V $.
    pwslnmlem2.b $e |- B e. _V $.
    pwslnmlem2.x $e |- X = ( W ^s A ) $.
    pwslnmlem2.y $e |- Y = ( W ^s B ) $.
    pwslnmlem2.z $e |- Z = ( W ^s ( A u. B ) ) $.
    pwslnmlem2.w $e |- ( ph -> W e. LMod ) $.
    pwslnmlem2.dj $e |- ( ph -> ( A i^i B ) = (/) ) $.
    pwslnmlem2.xn $e |- ( ph -> X e. LNoeM ) $.
    pwslnmlem2.yn $e |- ( ph -> Y e. LNoeM ) $.
    $( A sum of powers is Noetherian. $)
    pwslnmlem2 $p |- ( ph -> Z e. LNoeM ) $=
      ( vx wcel clnm eqid vy cbs cfv cv cres cmpt clmhm ccnv c0g csn cima cress
      crn clmod cun cvv wss unex a1i ssun1 pwssplit3 syl3anc cxp wceq crab fvex
      mptiniseg ax-mp cmnd cgrp lmodgrp grpmnd 3syl pws0g syl2anc eqcomd eqeq2d
      co rabbidv syl5eq oveq2d clmic wbr wb clmim cin pwssplit4 brlmici lnmlmic
      c0 syl mpbird eqeltrd wfo pwssplit1 forn ressid eqtrd lmhmlnmsplit ) AQGU
      BUCZQUDBUEZUFZGEUGVRRZGXBUHEUIUCZUJUKZULVRZSREXBUMZULVRZSRGSRADUNRZBCUOZU
      PRZBXJUQZXCMXKABCHIURUSZXLABCUTUSZQWTEUBUCZXJXBBDUPGELJWTTZXOTZXBTZVAVBAX
      FGXABDUIUCZUJVCZVDZQWTVEZULVRZSAXEYBGULAXEXAXDVDZQWTVEZYBXDUPRXEYEVDEUIVF
      QWTXBUPXDXAXRVGVHAYDYAQWTAXDXTXAAXTXDADVIRZBUPRZXTXDVDAXIDVJRYFMDVKDVLVMZ
      YGAHUSDBUPEXSJXSTZVNVOVPVQVSVTWAAYCSRZFSRZPAYCFWBWCZYKYJYKWDAUAYBUAUDCUEU
      FZYCFWEVRRZYLAXIXKBCWFWJVDYNMXMNUAQBCEFDGYMWTYBYCUPXSLXPYIYBTYMTJKYCTWGVB
      YCFYMWHWKPYCFSWIVOWLWMAXHESAXHEXOULVRZEAXGXOEULAWTXOXBWNZXGXOVDAYFXKXLYPY
      HXMXNQWTXOXJXBBDUPGELJXPXQXRWOVBWTXOXBWPWKWAAESRYOEVDOXOESXQWQWKWROWMGEXF
      XBXEXHXDXDTXETXFTXHTWSVB $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d W a b c $.  $d I a b c $.
    pwslnm.y $e |- Y = ( W ^s I ) $.
    $( Finite powers of Noetherian modules are Noetherian.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    pwslnm $p |- ( ( W e. LNoeM /\ I e. Fin ) -> Y e. LNoeM ) $=
      ( va vb vc clnm wcel wa cpws co cv wi c0 wceq oveq2 eleq1d imbi2d eqid wn
      cfn csn cun weq clmod lnmlmod pwslnmlem0 syl wel vex snex ad2antrl disjsn
      cin biimpri ad2antlr simprr pwslnmlem1 pwslnmlem2 exp32 findcard2s impcom
      a2d syl5eqel ) BHIZAUBIZJCBAKLZHDVGVFVHHIZVFBEMZKLZHIZNVFBOKLZHIZNVFBFMZK
      LZHIZNVFBVOGMZUCZUDZKLZHIZNVFVINEFGAVJOPZVLVNVFWCVKVMHVJOBKQRSEFUEZVLVQVF
      WDVKVPHVJVOBKQRSVJVTPZVLWBVFWEVKWAHVJVTBKQRSVJAPZVLVIVFWFVKVHHVJABKQRSVFB
      UFIZVNBUGZBVMVMTUHUIVOUBIZGFUJUAZJZVFVQWBWKVFVQWBWKVFVQJZJVOVSBVPBVSKLZWA
      FUKVRULVPTWMTZWATVFWGWKVQWHUMWJVOVSUOOPZWIWLWOWJVOVRUNUPUQWKVFVQURVFWMHIW
      KVQGBWMWNUSUMUTVAVDVBVCVE $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d Y a b c x y $.  $d S a b c x y $.  $d R a b c x y $.  $d B a b c x y $.
    $d Z a b c x y $.  $d I a b c x y $.  $d J a b c x y $.  $d F a b c x y $.
    $d ph a b c x y $.
    prdmgsum.y $e |- Y = ( S Xm_ R ) $.
    prdmgsum.b $e |- B = ( Base ` Y ) $.
    prdmgsum.z $e |- Z = ( 0g ` Y ) $.
    prdmgsum.i $e |- ( ph -> I e. V ) $.
    prdmgsum.j $e |- ( ph -> J e. W ) $.
    prdmgsum.s $e |- ( ph -> S e. X ) $.
    prdmgsum.r $e |- ( ph -> R : I --> CMnd ) $.
    prdmgsum.f $e |- ( ph -> F : J --> B ) $.
    prdmgsum.w $e |- ( ph -> ( `' F " ( _V \ { Z } ) ) e. Fin ) $.
    $( Finite commutative sums in a product structure are taken componentwise.
       (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    prdmgsum $p |- ( ph -> ( Y gsum F ) = ( x e. I |-> ( ( R ` x ) gsum
              ( y e. J |-> ( ( F ` y ) ` x ) ) ) ) ) $=
      ( va vb vc cgsu co cv cfv cmpt wfn wceq ccmn wf prdmcmnd gsumcl prdmbasfn
      ffn syl dffn5v sylib wcel wa ccom cbs cplusg c0g cvv adantr ffvelrn sylan
      eqid elex ad2antrr simpr simplr prdmbasprj fmptd cmnd cmnmnd mndidcl fvex
      fveq1 fvmpt wss ssriv sylancl prdm0g syl6eqr fveq1d 3eqtr2d simprl simprr
      fss fvco2 prdmplusgfval mndcl 3expb ad2antrl ad2antll oveq12d 3eqtr4d csn
      ccnv cdif cima cfn gsumhomo feqmptd eqidd fmptco 3eqtr3rd mpteq2dva eqtrd
      oveq2d ) AMGUGUHZBHBUIZXQUJZUKZBHXREUJZCIXRCUIZGUJZUJZUKZUGUHZUKAXQHULXQX
      TUMADEFXQHLJMOPTRAHUNEUOZEHULZUAHUNEUSUTZAIDGMKNPQAEFHLJMORTUAUPZSUBUCUQZ
      URBHXQVAVBABHXSYFAXRHVCZVDZYAUDDXRUDUIZUJZUKZGVEZUGUHXQYPUJZYFXSYMUEUFIDY
      AVFUJZMVGUJZYAVGUJZGMYAYPYAVHUJZVINPYSVMQUUBVMYTVMZUUAVMAMUNVCZYLYJVJAYGY
      LYAUNVCUAHUNXREVKVLAIVIVCZYLAIKVCUUESIKVNUTVJYMUDDYOYSYPYMYNDVCZVDDEFYNHX
      RLJMOPAFLVCZYLUUFTVOAHJVCZYLUUFRVOAYHYLUUFYIVOYMUUFVPAYLUUFVQVRYPVMZVSYMN
      YPUJZXRNUJZXRVHEVEZUJZUUBYMNDVCZUUJUUKUMAUUNYLAMVTVCZUUNAUUDUUOYJMWAUTZDM
      NPQWBUTVJUDNYOUUKDYPXRYNNWDUUIXRNWCWEUTAUUMUUKUMYLAXRUULNAUULMVHUJNAEFHLJ
      MORTAYGUNVTWFHVTEUOUAUDUNVTYNWAWGHUNVTEWOWHWIQWJWKVJAYHYLUUMUUBUMYIHVHEXR
      WPVLWLYMUEUIZDVCZUFUIZDVCZVDZVDZXRUUQUUSYTUHZUJZXRUUQUJZXRUUSUJZUUAUHUVCY
      PUJZUUQYPUJZUUSYPUJZUUAUHUVBDYTEFUUQUUSHXRLJMOPAUUGYLUVATVOAUUHYLUVARVOAY
      HYLUVAYIVOYMUURUUTWMYMUURUUTWNUUCAYLUVAVQWQUVBUVCDVCZUVGUVDUMYMUUOUVAUVJA
      UUOYLUUPVJUUOUURUUTUVJDYTMUUQUUSPUUCWRWSVLUDUVCYOUVDDYPXRYNUVCWDUUIXRUVCW
      CWEUTUVBUVHUVEUVIUVFUUAUURUVHUVEUMYMUUTUDUUQYOUVEDYPXRYNUUQWDUUIXRUUQWCWE
      WTUUTUVIUVFUMYMUURUDUUSYOUVFDYPXRYNUUSWDUUIXRUUSWCWEXAXBXCAIDGUOZYLUBVJZA
      GXEVINXDXFXGXHVCYLUCVJXIYMYQYEYAUGYMCUDIDYCYOYDGYPYMUVKYBIVCYCDVCUVLIDYBG
      VKVLAGCIYCUKUMYLACIDGUBXJVJYMYPXKXRYNYCWDXLXPYMXQDVCZYRXSUMAUVMYLYKVJUDXQ
      YOXSDYPXRYNXQWDUUIXRXQWCWEUTXMXNXO $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d Y a b c x y $.  $d R a b c x y $.  $d B a b c x y $.  $d Z a b c x y $.
    $d I a b c x y $.  $d J a b c x y $.  $d F a b c x y $.  $d ph a b c x y $.
    pwsgsum.y $e |- Y = ( R ^s I ) $.
    pwsgsum.b $e |- B = ( Base ` Y ) $.
    pwsgsum.z $e |- Z = ( 0g ` Y ) $.
    pwsgsum.i $e |- ( ph -> I e. V ) $.
    pwsgsum.j $e |- ( ph -> J e. W ) $.
    pwsgsum.r $e |- ( ph -> R e. CMnd ) $.
    pwsgsum.f $e |- ( ph -> F : J --> B ) $.
    pwsgsum.w $e |- ( ph -> ( `' F " ( _V \ { Z } ) ) e. Fin ) $.
    $( Finite commutative sums in a power structure are taken componentwise.
       (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    pwsgsum $p |- ( ph -> ( Y gsum F ) = ( x e. I |-> ( R gsum
              ( y e. J |-> ( ( F ` y ) ` x ) ) ) ) ) $=
      ( cgsu co csca cfv csn cprdm cv cmpt ccmn wcel wceq pwsval syl2anc oveq1d
      cxp eqid cbs cvv c0g fvex a1i wf fconst6g syl wb fveq2d syl5eq feq3 mpbid
      ccnv cdif cima sneqd difeq2d imaeq2d eqeltrrd prdmgsum wa fvconst2g sylan
      cfn mpteq2dva 3eqtrd ) AKFUAUBEUCUDZGEUEUOZUFUBZFUAUBBGBUGZWEUDZCHWGCUGFU
      DUDUHZUAUBZUHBGEWIUAUBZUHAKWFFUAAEUIUJZGIUJKWFUKRPEWDGUIIKMWDUPULUMZUNABC
      WFUQUDZWEWDFGHIJURWFWFUSUDZWFUPWNUPWOUPPQWDURUJAEUCUTVAAWLGUIWEVBRGEUIVCV
      DAHDFVBZHWNFVBZSADWNUKWPWQVEADKUQUDWNNAKWFUQWMVFVGDWNHFVHVDVIAFVJZURLUEZV
      KZVLWRURWOUEZVKZVLWAAWTXBWRAWSXAURALWOALKUSUDWOOAKWFUSWMVFVGVMVNVOTVPVQAB
      GWJWKAWGGUJZVRWHEWIUAAWLXCWHEUKRGEWGUIVSVTUNWBWC $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d ph a b c d e f g h $.  $d F a b c d e f g h $.  $d G a b c d e f g h $.
    $d H a b c d e f g h $.
    gsumpropd.f $e |- ( ph -> F e. V ) $.
    gsumpropd.g $e |- ( ph -> G e. W ) $.
    gsumpropd.h $e |- ( ph -> H e. X ) $.
    gsumpropd.b $e |- ( ph -> ( Base ` G ) = ( Base ` H ) ) $.
    gsumpropd.p $e |- ( ph -> ( +g ` G ) = ( +g ` H ) ) $.
    $( The group sum depends only on the base set and additive operation.  Note
       that for entirely unrestricted functions, there can be dependency on
       out-of-domain values of the operation, so this is somewhat weaker than
       ~ mndpropd etc.  (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    gsumpropd $p |- ( ph -> ( G gsum F ) = ( H gsum F ) ) $=
      ( ve vc vd vf cfv wceq wa cvv vg vh va vb cv cplusg cbs wral crab crn wss
      co c0g cdm cfz wcel cseq cuz wrex wex chash wf1o ccom ccnv cdif cima wsbc
      cio c1 cif csb cgsu oveqd eqeq1d anbi12d raleqbidv rabeqbidv csbeq1d fvex
      rabex biidd eqidd adantr elex syl grpidpropd seqeq2d fveq1d eqeq2d anbi2d
      rexbidv exbidv iotabidv sbcbidv ifbieq12d csbeq2dv mpan2 eqtrd fveq2 rneq
      sseq1d eleq1d seqeq3 wb cnveq imaeq1d dfsbcq coeq1 seqeq3d bitrd ifbieq2d
      dmeq df-gsum iotaex ifex csbex ovmpt2 syl2anc 3eqtr4d ) AMNUEZOUEZCUFQZUL
      ZYARZYAXTYBULZYARZSZOCUGQZUHZNYHUIZBUJZMUEZUKZCUMQZBUNZUOUJZUPZYOPUEZUAUE
      ZUOULZRZXTYSYBBYRUQZQZRZSZUAYRURQZUSZPUTZNVHZVIYAVAQZUOULYAUBUEZVBZXTUUJY
      BBUUKVCZVIUQZQZRZSZOBVDZTYLVEZVFZVGZUBUTZNVHZVJZVJZVKZMXTYADUFQZULZYARZYA
      XTUVGULZYARZSZODUGQZUHZNUVMUIZYMDUMQZYQUUAXTYSUVGBYRUQZQZRZSZUAUUFUSZPUTZ
      NVHZUULXTUUJUVGUUMVIUQZQZRZSZOUUTVGZUBUTZNVHZVJZVJZVKZCBVLULZDBVLULZAUVFM
      UVOUVEVKZUWMAMYJUVOUVEAYIUVNNYHUVMKAYGUVLOYHUVMKAYDUVIYFUVKAYCUVHYAAYBUVG
      XTYALVMVNAYEUVJYAAYBUVGYAXTLVMVNVOVPVQVRAUVOTUPZUWPUWMRUVNNUVMDUGVSVTZAMU
      VOUVEUWLTAYMYMYNUVDUVPUWKAYMWAAUCUDYHCDAYHWBKAUCUEZUDUEZYBULUWSUWTUVGULRU
      WSYHUPUWTYHUPSAYBUVGUWSUWTLVMWCACFUPCTUPZICFWDWEZADGUPDTUPZJDGWDWEZWFAYQY
      QUUIUVCUWCUWJAYQWAAUUHUWBNAUUGUWAPAUUEUVTUAUUFAUUDUVSUUAAUUCUVRXTAYSUUBUV
      QAYBUVGBYRLWGWHWIWJWKWLWMAUVBUWINAUVAUWHUBAUUQUWGOUUTAUUPUWFUULAUUOUWEXTA
      UUJUUNUWDAYBUVGUUMVILWGWHWIWJWNWLWMWOWOWPWQWRAUXABTUPZUWNUVFRUXBABEUPUXEH
      BEWDWEZUCUDCBTTMXTYAUWSUFQZULZYARZYAXTUXGULZYARZSZOUWSUGQZUHZNUXMUIZUWTUJ
      ZYLUKZUWSUMQZUWTUNZYPUPZUXSYTRZXTYSUXGUWTYRUQZQZRZSZUAUUFUSZPUTZNVHZUULXT
      UUJUXGUWTUUKVCZVIUQZQZRZSZOUWTVDZUUSVFZVGZUBUTZNVHZVJZVJZVKZUVFVLMYJUXQYN
      UXTUYAXTYSYBUWTYRUQZQZRZSZUAUUFUSZPUTZNVHZUULXTUUJYBUYIVIUQZQZRZSZOUYOVGZ
      UBUTZNVHZVJZVJZVKZUWSCRZVUAMYJUYTVKZVURVUSMUXOYJUYTVUSUXNYINUXMYHUWSCUGWS
      ZVUSUXLYGOUXMYHVVAVUSUXIYDUXKYFVUSUXHYCYAVUSUXGYBXTYAUWSCUFWSZVMVNVUSUXJY
      EYAVUSUXGYBYAXTVVBVMVNVOVPVQVRVUSYJTUPZVUTVURRYINYHCUGVSVTZVUSMYJUYTVUQTV
      USUXQUXQUXRUYSYNVUPVUSUXQWAUWSCUMWSVUSUXTUXTUYHUYRVUHVUOVUSUXTWAVUSUYGVUG
      NVUSUYFVUFPVUSUYEVUEUAUUFVUSUYDVUDUYAVUSUYCVUCXTVUSYSUYBVUBVUSUXGYBUWTYRV
      VBWGWHWIWJWKWLWMVUSUYQVUNNVUSUYPVUMUBVUSUYMVULOUYOVUSUYLVUKUULVUSUYKVUJXT
      VUSUUJUYJVUIVUSUXGYBUYIVIVVBWGWHWIWJWNWLWMWOWOWPWQWRUWTBRZVVCVURUVFRVVDVV
      EMYJVUQUVETVVEUXQYMVUPUVDYNVVEUXPYKYLUWTBWTXAZVVEUXTYQVUHVUOUUIUVCVVEUXSY
      OYPUWTBXLZXBZVVEVUGUUHNVVEVUFUUGPVVEVUEUUEUAUUFVVEUYAUUAVUDUUDVVEUXSYOYTV
      VGVNZVVEVUCUUCXTVVEYSVUBUUBYBUWTBYRXCWHWIVOWKWLWMVVEVUNUVBNVVEVUMUVAUBVVE
      VUMVULOUUTVGZUVAVVEUYOUUTRZVUMVVJXDVVEUYNUURUUSUWTBXEXFZVULOUYOUUTXGWEVVE
      VULUUQOUUTVVEVUKUUPUULVVEVUJUUOXTVVEUUJVUIUUNVVEUYIUUMYBVIUWTBUUKXHZXIWHW
      IWJWNXJWLWMWOXKWPWQNOUCUDUBPUAMXMZMYJUVEVVDYMYNUVDCUMVSYQUUIUVCUUHNXNUVBN
      XNXOXOXPXQXRAUXCUXEUWOUWMRUXDUXFUCUDDBTTVUAUWMVLMUVOUXQUVPUXTUYAXTYSUVGUW
      TYRUQZQZRZSZUAUUFUSZPUTZNVHZUULXTUUJUVGUYIVIUQZQZRZSZOUYOVGZUBUTZNVHZVJZV
      JZVKZUWSDRZVUAMUVOUYTVKZVWKVWLMUXOUVOUYTVWLUXNUVNNUXMUVMUWSDUGWSZVWLUXLUV
      LOUXMUVMVWNVWLUXIUVIUXKUVKVWLUXHUVHYAVWLUXGUVGXTYAUWSDUFWSZVMVNVWLUXJUVJY
      AVWLUXGUVGYAXTVWOVMVNVOVPVQVRVWLUWQVWMVWKRUWRVWLMUVOUYTVWJTVWLUXQUXQUXRUY
      SUVPVWIVWLUXQWAUWSDUMWSVWLUXTUXTUYHUYRVWAVWHVWLUXTWAVWLUYGVVTNVWLUYFVVSPV
      WLUYEVVRUAUUFVWLUYDVVQUYAVWLUYCVVPXTVWLYSUYBVVOVWLUXGUVGUWTYRVWOWGWHWIWJW
      KWLWMVWLUYQVWGNVWLUYPVWFUBVWLUYMVWEOUYOVWLUYLVWDUULVWLUYKVWCXTVWLUUJUYJVW
      BVWLUXGUVGUYIVIVWOWGWHWIWJWNWLWMWOWOWPWQWRVVEUWQVWKUWMRUWRVVEMUVOVWJUWLTV
      VEUXQYMVWIUWKUVPVVFVVEUXTYQVWAVWHUWCUWJVVHVVEVVTUWBNVVEVVSUWAPVVEVVRUVTUA
      UUFVVEUYAUUAVVQUVSVVIVVEVVPUVRXTVVEYSVVOUVQUVGUWTBYRXCWHWIVOWKWLWMVVEVWGU
      WINVVEVWFUWHUBVVEVWFVWEOUUTVGZUWHVVEVVKVWFVWPXDVVLVWEOUYOUUTXGWEVVEVWEUWG
      OUUTVVEVWDUWFUULVVEVWCUWEXTVVEUUJVWBUWDVVEUYIUUMUVGVIVVMXIWHWIWJWNXJWLWMW
      OXKWPWQVVNMUVOUWLUWRYMUVPUWKDUMVSYQUWCUWJUWBNXNUWINXNXOXOXPXQXRXS $.
      $( [1-Feb-2015] $)
  $}

