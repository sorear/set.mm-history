$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Miscellanea for polynomials
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x a b $.  $d V a b $.  $d B a b $.  $d C a b $.  $d R a b x $.
    $( Value of a pointwise operation on two functions defined using maps-to
       notation.  (Contributed by Stefan O'Rear, 5-Oct-2014.) $)
    ofmpteq $p |- ( ( A e. V /\ ( x e. A |-> B ) Fn A /\ ( x e. A |-> C ) Fn A
        ) ->
        ( ( x e. A |-> B ) oF R ( x e. A |-> C ) ) = ( x e. A |-> ( B R C ) ) )
        $=
      ( va vb wcel cmpt wfn co cv csb cvv wral eqid mptfng ax-17 cbvmpt w3a cof
      simp1 wa simpr simpl2 sylibr ax17el hbcsb1 hbel csbeq1a eleq1d rcla4 sylc
      weq simpl3 wceq a1i offval2 hbov oveq12d syl6eqr ) BFIZABCJZBKZABDJZBKZUA
      ZVDVFEUBLGBAGMZCNZAVIDNZELZJABCDELZJVHGBVJVKEVDVFFOOVCVEVGUCVHVIBIZUDZVNC
      OIZABPZVJOIZVHVNUEZVOVEVQVCVEVGVNUFABCVDVDQRUGVPVRAVIBAHHVJOAHVICHGAUHZUI
      ZHMZOIASZUJAGUOZCVJOAVICUKZULUMUNVOVNDOIZABPZVKOIZVSVOVGWGVCVEVGVNUPABDVF
      VFQRUGWFWHAVIBAHHVKOAHVIDVTUIZWCUJWDDVKOAVIDUKZULUMUNVDGBVJJUQVHAGHBCVJWB
      CIGSWAWETURVFGBVKJUQVHAGHBDVKWBDIGSWIWJTURUSAGHBVMVLWBVMIGSAHVJVKEWAWBEIA
      SWIUTWDCVJDVKEWEWJVATVB $.
      $( [5-Oct-2014] $)
  $}

  ${
    $d A t $.  $d C t $.
    $( Interpret range of a maps-to notation as a constraint on the
       definition.  (Contributed by Stefan O'Rear, 10-Oct-2014.) $)
    mptfcl $p |- ( ( t e. A |-> B ) : A --> C -> ( t e. A -> B e. C ) ) $=
      ( cmpt wf wcel wral cv wi eqid fmpt ra4 sylbir ) BDABCEZFCDGZABHAIBGPJABD
      COOKLPABMN $.
      $( [10-Oct-2014] $)
  $}

  ${
    $d F a b c $.  $d A a b c $.  $d G a b c $.  $d V a b c $.  $d X a b c $.
    $d R a b c $.

    $( Function value of a pointwise composition.  (Contributed by Stefan
       O'Rear, 5-Oct-2014.) $)
    fnfvof $p |- ( ( ( F Fn A /\ G Fn A ) /\ ( A e. V /\ X e. A ) ) ->
        ( ( F oF R G ) ` X ) = ( ( F ` X ) R ( G ` X ) ) ) $=
      ( vc va vb wfn wa wcel co cfv cdm cin cv cmpt cvv wceq cof fnex ad2ant2lr
      ad2ant2r fndm ineqan12d adantr inidm syl6eq simprl mptexg syl dmeq ineq1d
      eqeltrd fveq1 oveq1d mpteq12dv ineq2d oveq2d df-of ovmpt2g syl3anc fveq1d
      simprr eleqtrrd fveq2 oveq12d eqid ovex fvmpt eqtrd ) CAJZDAJZKZAELZFALZK
      ZKZFCDBUAZMZNFGCOZDOZPZGQZCNZWEDNZBMZRZNZFCNZFDNZBMZVSFWAWIVSCSLZDSLZWISL
      ZWAWITVMVPWNVNVQAECUBUDVNVPWOVMVQAEDUBUCVSWDELWPVSWDAEVSWDAAPZAVOWDWQTVRV
      MVNWBAWCAACUEADUEUFUGAUHUIZVOVPVQUJUOGWDWHEUKULHICDSSGHQZOZIQZOZPZWEWSNZW
      EXANZBMZRWIVTGWBXBPZWFXEBMZRSWSCTZGXCXFXGXHXIWTWBXBWSCUMUNXIXDWFXEBWEWSCU
      PUQURXADTZGXGXHWDWHXJXBWCWBXADUMUSXJXEWGWFBWEXADUPUTURGBHIVAVBVCVDVSFWDLW
      JWMTVSFAWDVOVPVQVEWRVFGFWHWMWDWIWEFTWFWKWGWLBWEFCVGWEFDVGVHWIVIWKWLBVJVKU
      LVL $.
      $( [5-Oct-2014] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $d x y D $.  $d x E $.
    $( Express composition of two functions as a maps-to applying both in
       sequence.  (Contributed by Stefan O'Rear, 5-Oct-2014.)  (The proof was
       shortened by Mario Carneiro, 27-Dec-2014.) $)
    fcompt $p |- ( ( A : D --> E /\ B : C --> D ) -> ( A o. B ) = ( x e. C |->
        ( A ` ( B ` x ) ) ) ) $=
      ( vy wf wa cv cfv wcel ffvelrn adantll wfn cmpt wceq ffn dffn5v sylib
      adantl adantr fveq2 fmptco ) EFBHZDECHZIZAGDEAJZCKZGJZBKZUIBKCBUFUHDLUIEL
      UEDEUHCMNUGCDOZCADUIPQUFULUEDECRUAADCSTUGBEOZBGEUKPQUEUMUFEFBRUBGEBSTUJUI
      BUCUD $.
      $( [27-Dec-2014] $) $( [5-Oct-2014] $)
  $}


