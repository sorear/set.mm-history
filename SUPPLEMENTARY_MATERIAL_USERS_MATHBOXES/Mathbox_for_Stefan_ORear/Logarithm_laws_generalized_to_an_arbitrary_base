$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Logarithm laws generalized to an arbitrary base
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c d e f g A $.  $d a b c d e f g B $.  $d a b c d e f g C $.
    $d a b c d e f g D $.  $d a b c d e f g E $.  $d a b c d e f g F $.
    $d a b c d e f g u $.  $d a b c d e f g v $.  $d a b c d e f g w $.
    $d a b c d e f g x $.  $d a b c d e f g y $.  $d a b c d e f g z $.
    $d a b c d e f g ph $.

    $( Logarithm of a non-1 number is not zero and thus suitible as a divisor.
       We will be representing the general-base logarithm as a quotient of
       natural logarithms.  (Contributed by Stefan O'Rear, 19-Sep-2014.) $)
    logne0 $p |- ( ( A e. RR+ /\ A =/= 1 ) -> ( log ` A ) =/= 0 ) $=
      ( crp wcel c1 wne wa clog cfv cc0 ce simpr wceq ef0 neeqtrrd necomd cr wb
      a1i 0re relogeftb syldan necon3bid mpbird ) ABCZADEZFZAGHZIEIJHZAEUFAUHUF
      ADUHUDUEKUHDLUFMRNOUFUGIUHAUDUEIPCZUGILUHALQUIUFSRAITUAUBUC $.
      $( [19-Sep-2014] $)

    $( General logarithm is a real number.  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    reglogcl $p |- ( ( A e. RR+ /\ B e. RR+ /\ B =/= 1 ) -> ( ( log ` A ) / (
        log ` B ) ) e. RR ) $=
      ( crp wcel c1 wne w3a clog cfv cr cc0 co relogcl 3ad2ant1 3ad2ant2 logne0
      cdiv 3adant1 redivcl syl3anc ) ACDZBCDZBEFZGAHIZJDZBHIZJDZUFKFZUDUFQLJDUA
      UBUEUCAMNUBUAUGUCBMOUBUCUHUABPRUDUFST $.
      $( [19-Sep-2014] $)

    $( General logarithm preserves "less than".  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    reglogltb $p |- ( ( ( A e. RR+ /\ B e. RR+ ) /\ ( C e. RR+ /\ 1 < C ) ) ->
        ( A < B <-> ( ( log ` A ) / ( log ` C ) ) < ( ( log ` B ) / ( log ` C )
        ) ) ) $=
      ( crp wcel wa c1 clt wbr clog cfv cdiv co wb logltb adantr cr cc0 relogcl
      ad2antrr ad2antlr ad2antrl log1 mpan biimpa syl5eqbrr adantl ltdiv1 bitrd
      1rp syl112anc ) ADEZBDEZFZCDEZGCHIZFZFZABHIZAJKZBJKZHIZUTCJKZLMVAVCLMHIZU
      NUSVBNUQABOPURUTQEZVAQEZVCQEZRVCHIZVBVDNULVEUMUQASTUMVFULUQBSUAUOVGUNUPCS
      UBUQVHUNUQRGJKZVCHUCUOUPVIVCHIZGDEUOUPVJNUJGCOUDUEUFUGUTVAVCUHUKUI $.
      $( [19-Sep-2014] $)

    $( Natural logarithm preserves ` <_ ` .  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    logleb $p |- ( ( A e. RR+ /\ B e. RR+ ) -> ( A <_ B <-> ( log ` A ) <_ (
        log ` B ) ) ) $=
      ( crp wcel wa clt wbr wn clog cfv cle wb logltb ancoms notbid rpre syl2an
      cr lenlt relogcl 3bitr4d ) ACDZBCDZEZBAFGZHZBIJZAIJZFGZHZABKGZUHUGKGZUDUE
      UIUCUBUEUILBAMNOUBARDBRDUKUFLUCAPBPABSQUBUHRDUGRDULUJLUCATBTUHUGSQUA $.
      $( [19-Sep-2014] $)

    $( General logarithm preserves ` <_ ` .  (Contributed by Stefan O'Rear,
       19-Oct-2014.) $)
    reglogleb $p |- ( ( ( A e. RR+ /\ B e. RR+ ) /\ ( C e. RR+ /\ 1 < C ) ) ->
        ( A <_ B <-> ( ( log ` A ) / ( log ` C ) ) <_ ( ( log ` B ) / ( log ` C
        ) ) ) ) $=
      ( crp wcel wa c1 clt wbr cle clog cfv cdiv co wb logleb adantr cr relogcl
      cc0 ad2antrr ad2antlr ad2antrl log1 logltb biimpa syl5eqbrr adantl lediv1
      1rp mpan syl112anc bitrd ) ADEZBDEZFZCDEZGCHIZFZFZABJIZAKLZBKLZJIZVBCKLZM
      NVCVEMNJIZUPVAVDOUSABPQUTVBREZVCREZVEREZTVEHIZVDVFOUNVGUOUSASUAUOVHUNUSBS
      UBUQVIUPURCSUCUSVJUPUSTGKLZVEHUDUQURVKVEHIZGDEUQURVLOUJGCUEUKUFUGUHVBVCVE
      UIULUM $.
      $( [19-Oct-2014] $)

    $( Multiplication law for general log.  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    reglogmul $p |- ( ( A e. RR+ /\ B e. RR+ /\ ( C e. RR+ /\ C =/= 1 ) ) -> (
        ( log ` ( A x. B ) ) / ( log ` C ) ) = ( ( ( log ` A ) / ( log ` C ) )
        + ( ( log ` B ) / ( log ` C ) ) ) ) $=
      ( crp wcel c1 wne wa w3a cmul co clog cdiv caddc wceq cc relogcl 3ad2ant3
      cfv recnd relogmul 3adant3 oveq1d 3ad2ant1 adantr logne0 divdir syl112anc
      cc0 3ad2ant2 eqtrd ) ADEZBDEZCDEZCFGZHZIZABJKLSZCLSZMKALSZBLSZNKZUSMKZUTU
      SMKVAUSMKNKZUQURVBUSMULUMURVBOUPABUAUBUCUQUTPEZVAPEZUSPEZUSUIGZVCVDOULUMV
      EUPULUTAQTUDUMULVFUPUMVABQTUJUPULVGUMUNVGUOUNUSCQTUERUPULVHUMCUFRUTVAUSUG
      UHUK $.
      $( [19-Sep-2014] $)

    $( Power law for general log.  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    reglogexp $p |- ( ( A e. RR+ /\ N e. ZZ /\ ( C e. RR+ /\ C =/= 1 ) ) -> ( (
        log ` ( A ^ N ) ) / ( log ` C ) ) = ( N x. ( ( log ` A ) / ( log ` C )
        ) ) ) $=
      ( crp wcel cz c1 wne wa w3a co clog cdiv cmul wceq relogcl recnd 3ad2ant3
      cfv cc cexp relogexp 3adant3 oveq1d cc0 zcn 3ad2ant2 adantr logne0 divass
      3ad2ant1 syl112anc eqtrd ) ADEZCFEZBDEZBGHZIZJZACUAKLSZBLSZMKCALSZNKZVAMK
      ZCVBVAMKNKZUSUTVCVAMUNUOUTVCOURACUBUCUDUSCTEZVBTEZVATEZVAUEHZVDVEOUOUNVFU
      RCUFUGUNUOVGURUNVBAPQUKURUNVHUOUPVHUQUPVABPQUHRURUNVIUOBUIRCVBVAUJULUM $.
      $( [19-Sep-2014] $)

    $( General log of the base is 1.  (Contributed by Stefan O'Rear,
       19-Sep-2014.) $)
    reglogbas $p |- ( ( C e. RR+ /\ C =/= 1 ) -> ( ( log ` C ) / ( log ` C ) )
        = 1 ) $=
      ( crp wcel c1 wne wa clog cfv cc cdiv co wceq relogcl recnd adantr logne0
      cc0 divid syl2anc ) ABCZADEZFAGHZICZUBQEUBUBJKDLTUCUATUBAMNOAPUBRS $.
      $( [19-Sep-2014] $)

    $( General log of 1 is 0.  (Contributed by Stefan O'Rear, 19-Sep-2014.) $)
    reglog1 $p |- ( ( C e. RR+ /\ C =/= 1 ) -> ( ( log ` 1 ) / ( log ` C ) ) =
        0 ) $=
      ( crp wcel c1 wne wa clog cfv cdiv co cc0 log1 oveq1i wceq relogcl adantr
      cc recnd logne0 div0 syl2anc syl5eq ) ABCZADEZFZDGHZAGHZIJKUGIJZKUFKUGILM
      UEUGQCZUGKEUHKNUCUIUDUCUGAORPASUGTUAUB $.
      $( [19-Sep-2014] $)

    $( General log of a power of the base is the exponent.  (Contributed by
       Stefan O'Rear, 19-Sep-2014.) $)
    reglogexpbas $p |- ( ( N e. ZZ /\ ( C e. RR+ /\ C =/= 1 ) ) -> ( ( log ` (
        C ^ N ) ) / ( log ` C ) ) = N ) $=
      ( cz wcel crp c1 wne wa cexp co clog cfv cdiv cmul simprl simpl reglogexp
      wceq simpr syl3anc reglogbas adantl oveq2d zcn adantr mulid1 syl 3eqtrd
      cc ) BCDZAEDZAFGZHZHZABIJKLAKLZMJZBUOUOMJZNJZBFNJZBUNUKUJUMUPURRUJUKULOUJ
      UMPUJUMSAABQTUNUQFBNUMUQFRUJAUAUBUCUNBUIDZUSBRUJUTUMBUDUEBUFUGUH $.
      $( [19-Sep-2014] $)
  $}

