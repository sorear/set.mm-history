$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Principal ideal rings.  Divisibility in the integers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LPIdeal LPIR $.

  $( Ring left-principal-ideal function. $)
  clpidl $a class LPIdeal $.

  $( Class of left principal ideal rings. $)
  clpir $a class LPIR $.

  ${
    $d w i g $.

    $( Define the class of left principal ideals of a ring, which are ideals
       with a single generator. $)
    df-lpidl $a |- LPIdeal = ( w e. Ring |->
        U_ g e. ( Base ` w ) { ( ( RSpan ` w ) ` { g } ) } ) $.

    $( Define the class of left principal ideal rings, rings where every left
       ideal has a single generator. $)
    df-lpir $a |- LPIR = { w e. Ring | ( LIdeal ` w ) = ( LPIdeal ` w ) } $.
  $}


  $( All function values are either null or in the range. $)
  fvelrn0 $p |- ( F ` X ) e. ( ran F u. { (/) } ) $=
    ( cvv wcel cfv crn c0 csn cun wbr wceq wo fvbr0 wa simpr fvex simpl brelrng
    a1i syl3anc elun1 syl 0ex snid elun2 ax-mp eleq1 mpbiri adantr jaoian fvprc
    mpan wn syl6eqel pm2.61i ) BCDZBAEZAFZGHZIZDZBUQAJZUQGKZLUPVAABMVBUPVAVCVBU
    PNZUQURDZVAVDUPUQCDZVBVEVBUPOVFVDBAPSVBUPQBUQACCRTUQURUSUAUBVCVAUPVCVAGUTDZ
    GUSDVGGUCUDGUSURUEUFZUQGUTUGUHUIUJULUPUMUQGUTBAUKVHUNUO $.
    $( [3-Jan-2015] $)

  ${
    $d R r g $.  $d P r g $.  $d B r g $.  $d r K g $.  $d U g $.  $d I g $.
    $d Z g $.  $d B g $.
    lpival.p $e |- P = ( LPIdeal ` R ) $.
    ${
      lpival.k $e |- K = ( RSpan ` R ) $.
      lpival.b $e |- B = ( Base ` R ) $.
      $( Value of the set of principal ideals. $)
      lpival $p |- ( R e. Ring -> P = U_ g e. B { ( K ` { g } ) } ) $=
        ( vr crg wcel clpidl cfv cbs cv csn crsp ciun wceq fveq2 wral ralrimivw
        fveq1d sneqd iuneq1 iuneq2 sylan9eq syl2anc df-lpidl crn fvex rnex p0ex
        c0 cun unex wss iunss fvelrn0 snssi ax-mp a1i mprgbir ssexi fvmpt sneqi
        fveq1i iuneq2i eqtri 3eqtr4g ) CJKCLMDCNMZDOZPZCQMZMZPZRZBDAVMEMZPZRZIC
        DIOZNMZVMWAQMZMZPZRZVQJLWACSZWBVKSZWEVPSZDVKUAZWFVQSWACNTWGWIDVKWGWDVOW
        GVMWCVNWACQTUCUDUBWHWJWFDVKWERVQDWBVKWEUEDVKWEVPUFUGUHIDUIVQVNUJZUNPZUO
        ZWKWLVNCQUKULUMUPVQWMUQVPWMUQZDVKDVKVPWMURWNVLVKKZVOWMKWNVNVMUSVOWMUTVA
        VBVCVDVEFVTDVKVSRZVQAVKSVTWPSHDAVKVSUEVADVKVSVPVSVPSWOVRVOVMEVNGVGVFVBV
        HVIVJ $.
        $( [3-Jan-2015] $)

      $( Property of being a principal ideal. $)
      islpidl $p |- ( R e. Ring -> ( I e. P <-> E. g e. B
          I = ( K ` { g } ) ) ) $=
        ( crg wcel cv csn cfv ciun wceq wrex lpival eleq2d eliun elsnc2 rexbii
        fvex bitri syl6bb ) CJKZEBKEDADLMZFNZMZOZKZEUHPZDAQZUFBUJEABCDFGHIRSUKE
        UIKZDAQUMDEAUITUNULDAEUHUGFUCUAUBUDUE $.
        $( [3-Jan-2015] $)
    $}

    ${
      lpi0.z $e |- Z = ( 0g ` R ) $.
      $( The zero ideal is always principal. $)
      lpi0 $p |- ( R e. Ring -> { Z } e. P ) $=
        ( vg crg wcel csn crsp cfv wceq cbs wrex eqid rng0cl rsp0 eqcomd sneq
        cv fveq2d eqeq2d rcla4ev syl2anc islpidl mpbird ) BGHZCIZAHUHFTZIZBJKZK
        ZLZFBMKZNZUGCUNHUHUHUKKZLZUOUNBCUNOZEPUGUPUHBUKCUKOZEQRUMUQFCUNUICLZULU
        PUHUTUJUHUKUICSUAUBUCUDUNABFUHUKDUSURUEUF $.
        $( [3-Jan-2015] $)
    $}

    ${
      lpi1.b $e |- B = ( Base ` R ) $.
      $( The unit ideal is always principal. $)
      lpi1 $p |- ( R e. Ring -> B e. P ) $=
        ( vg crg wcel csn crsp cfv wceq wrex cur eqid rngidcl rsp1 eqcomd sneq
        cv fveq2d eqeq2d rcla4ev syl2anc islpidl mpbird ) CGHZABHAFTZIZCJKZKZLZ
        FAMZUGCNKZAHAUNIZUJKZLZUMACUNEUNOZPUGUPAACUJUNUJOZEURQRULUQFUNAUHUNLZUK
        UPAUTUIUOUJUHUNSUAUBUCUDABCFAUJDUSEUEUF $.
        $( [3-Jan-2015] $)
    $}

    ${
      $d U r $.
      lpiss.u $e |- U = ( LIdeal ` R ) $.
      $( Principal ideal rings are where all ideals are principal. $)
      islpir $p |- ( R e. LPIR <-> ( R e. Ring /\ U = P ) ) $=
        ( vr cv clidl cfv clpidl wceq crg clpir eqeq12d eqeq12i syl6bbr df-lpir
        fveq2 elrab2 ) FGZHIZTJIZKZCAKZFBLMTBKZUCBHIZBJIZKUDUEUAUFUBUGTBHRTBJRN
        CUFAUGEDOPFQS $.
        $( [3-Jan-2015] $)

      $d P a g $.  $d U a g $.  $d R a g $.
      $( Principal ideals are a subclass of ideal. $)
      lpiss $p |- ( R e. Ring -> P C_ U ) $=
        ( va vg crg wcel cv csn crsp cfv wceq cbs wrex eqid islpidl wa wss
        snssi eqid1 rspcl sylan2 eleq1 syl5ibrcom rexlimdva sylbid ssrdv ) BHIZ
        FACUJFJZAIUKGJZKZBLMZMZNZGBOMZPUKCIZUQABGUKUNDUNQUQQZRUJUPURGUQUJULUQIZ
        SURUPUOCIZUTUJUMUQTVAULUQUAUQBCUMUNUNUBUSEUCUDUKUOCUEUFUGUHUI $.
        $( [3-Jan-2015] $)

      $( Principal ideal rings are where all ideals are principal. $)
      islpir2 $p |- ( R e. LPIR <-> ( R e. Ring /\ U C_ P ) ) $=
        ( clpir wcel crg wceq wa wss lpiss biantrud eqss syl6rbbr pm5.32i bitri
        islpir ) BFGBHGZCAIZJSCAKZJABCDERSTUASUAUAACKZJTSUBUAABCDELMCANOPQ $.
        $( [3-Jan-2015] $)
    $}
  $}

  $( Division rings are principal ideal. $)
  drnglpir $p |- ( R e. DivRing -> R e. LPIR ) $=
    ( cdr wcel crg clidl cfv clpidl wss clpir drngrng c0g csn cbs eqid drngnidl
    cpr lpi0 lpi1 wa sylanbrc snex fvex prss bicomi syl eqsstrd islpir2 ) ABCZA
    DCZAEFZAGFZHAICAJZUHUJAKFZLZAMFZPZUKUOAUJUMUONZUMNZUJNZOUHUIUPUKHZULUIUNUKC
    ZUOUKCZUTUKAUMUKNZURQUOUKAVCUQRVAVBSUTUNUOUKUMUAAMUBUCUDTUEUFUKAUJVCUSUGT
    $.
    $( [3-Jan-2015] $)

  ${
    $d a x R $.  $d G a x $.  $d B a x $.  $d K a x $.  $d D a x $.
    rspsn.b $e |- B = ( Base ` R ) $.
    rspsn.k $e |- K = ( RSpan ` R ) $.
    rspsn.d $e |- D = ( ||r ` R ) $.
    $( Membership in principal ideals is closely related to divisibility. $)
    rspsn $p |- ( ( R e. Ring /\ G e. B ) ->
        ( K ` { G } ) = { x | G D x } ) $=
      ( va crg wcel cfv cv co wceq cbs wrex adantr eqid csn crglmod clspn cvsca
      wa csca cab crsp rspval syl5eq fveq1d clmod rlmlmod rlmbase eleq2d biimpa
      wbr lspsn syl2anc cmulr cvv eleq2i biimpi adantl vex eqid1 dvdsr2 sylancl
      wb rlmsca fveq2d eqcom oveqd eqeq2d syl5bb rexeqbidv bitr2d abbidv 3eqtrd
      rlmvsca ) DKLZEBLZUEZEUAZFMWDDUBMZUCMZMZANZJNZEWEUDMZOZPZJWEUFMZQMZRZAUGZ
      EWHCUQZAUGWCWDFWFWCFDUHMZWFHWAWRWFPWBDKUISUJUKWCWEULLZEWEQMZLZWGWPPWAWSWB
      DUMSWAWBXAWABWTEWABDQMZWTGDKUNUJUOUPAWJJWMWNWFWTWEEWMTWNTWTTWJTWFTURUSWCW
      OWQAWCWQWIEDUTMZOZWHPZJXBRZWOWCEXBLZWHVALWQXFVIWBXGWAWBXGBXBEGVBVCVDAVEJX
      BCDXCVAEWHXBTIXCVFVGVHWAXFWOVIWBWAXEWLJXBWNWADWMQDKVJVKXEWHXDPWAWLXDWHVLW
      AXDWKWHWAXCWJWIEDKVTVMVNVOVPSVQVRVS $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d U x y $.  $d B x y $.  $d D x y $.  $d R x y $.  $d I x y $.
    $d K x y $.  $d G x y $.
    lidldvgen.b $e |- B = ( Base ` R ) $.
    lidldvgen.u $e |- U = ( LIdeal ` R ) $.
    lidldvgen.k $e |- K = ( RSpan ` R ) $.
    lidldvgen.d $e |- D = ( ||r ` R ) $.
    $( An element generates an ideal iff it is contained in the ideal and all
       elements are right-divided by it. $)
    lidldvgen $p |- ( ( R e. Ring /\ I e. U /\ G e. B ) ->
        ( I = ( K ` { G } ) <-> ( G e. I /\ A. x e. I G D x ) ) ) $=
      ( vy wcel wceq cv wbr wral wa wss crg w3a csn simp1 simp3 rspssid syl2anc
      cfv snssd snssg 3ad2ant3 mpbird cab rspsn 3adant2 eleq2d vex breq2 syl6bb
      wb elab biimpd ralrimiv jca eleq2 raleq anbi12d syl5ibrcom wi df-ral ssab
      bitr4i biimpi ad2antll adantr sseqtr4d simpl1 simpl2 snssi adantl syl3anc
      wal rspssp adantrr eqssd ex impbid ) DUANZGENZFBNZUBZGFUCZHUHZOZFGNZFAPZC
      QZAGRZSZWKWSWNFWMNZWQAWMRZSWKWTXAWKWTWLWMTZWKWHWLBTXBWHWIWJUDWKFBWHWIWJUE
      UIBDWLHKIUFUGWJWHWTXBUTWIFWMBUJUKULWKWQAWMWKWPWMNZWQWKXCWPFMPZCQZMUMZNWQW
      KWMXFWPWHWJWMXFOWIMBCDFHIKLUNUOUPXEWQMWPAUQXDWPFCURVAUSVBVCVDWNWOWTWRXAGW
      MFVEWQAGWMVFVGVHWKWSWNWKWSSZGWMXGGWQAUMZWMWRGXHTZWKWOWRXIWRWPGNWQVIAWBXIW
      QAGVJWQAGVKVLVMVNWKWMXHOZWSWHWJXJWIABCDFHIKLUNUOVOVPWKWOWMGTZWRWKWOSWHWIW
      LGTZXKWHWIWJWOVQWHWIWJWOVRWOXLWKFGVSVTDEWLGHKJWCWAWDWEWFWG $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d R x y $.  $d I x y $.  $d U x y $.  $d P x y $.  $d D x y $.
    lpigen.u $e |- U = ( LIdeal ` R ) $.
    lpigen.p $e |- P = ( LPIdeal ` R ) $.
    lpigen.d $e |- D = ( ||r ` R ) $.
    $( An ideal is principal iff it contains an element which right-divides all
       elements. $)
    lpigen $p |- ( ( R e. Ring /\ I e. U ) -> ( I e. P <->
        E. x e. I A. y e. I x D y ) ) $=
      ( crg wcel wa cv csn crsp cfv wrex wb eqid wceq cbs wbr islpidl lidldvgen
      adantr 3expa rexbidva simpr lidlss sseld adantrd impbid2 rexbidv2 3bitrd
      wral ancrd ) EKLZGFLZMZGDLZGANZOEPQZQUAZAEUBQZRZVBGLZVBBNCUCBGUPZMZAVERVH
      AGRURVAVFSUSVEDEAGVCIVCTZVETZUDUFUTVDVIAVEURUSVBVELZVDVISBVECEFVBGVCVKHVJ
      JUEUGUHUTVIVHAVEGUTVLVIMVIVLVIUIUTVIVLUTVGVLVHUTGVEVBVEGFKEVKHUJUKULUQUMU
      NUO $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d Z x y z $.
    dvdsrz.z $e |- Z = ( CCfld |`s ZZ ) $.
    $( Ring divisibility in ` ZZ ` corresponds to ordinary divisibility. $)
    dvdsrz $p |- || = ( ||r ` Z ) $=
      ( vx vy vz cv cz wcel wa cmul co wceq copab cfv simpl cvv ccnfld cress cc
      ax-mp wrex cdivides cdsr anim1i zmulcl ancoms eleq1 syl5ibcom simpr jca31
      rexlimdva imp impbii opabbii df-divides eqeltri wss cbs cnfldbas ressbas2
      ovex zsscn eqid cnfldmul ressmulr dvdsrval 3eqtr4i ) CFZGHZDFZGHZIZEFZVHJ
      KZVJLZEGUAZIZCDMVIVPIZCDMZUBAUCNZVQVRCDVQVRVLVIVPVIVKOUDVRVIVKVPVIVPOVIVP
      VKVIVOVKEGVIVMGHZIVNGHZVOVKWAVIWBVMVHUEUFVNVJGUGUHUKULVIVPUIUJUMUNCDEUOAP
      HVTVSLAQGRKPBQGRVAUPCDEGVTAJPGSUQGAURNLVBGSAQBUSUTTVTVCGQAJBVDVEVFTVG $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d Z a $.  $d I a $.  $d ph a $.  $d X a $.  $d G a $.
    zlpirlem.z $e |- Z = ( CCfld |`s ZZ ) $.
    zlpirlem.i $e |- ( ph -> I e. ( LIdeal ` Z ) ) $.
    zlpirlem.n0 $e |- ( ph -> I =/= { 0 } ) $.
    $( Lemma for ~ zlpir .  A nonzero ideal of integers contains some positive
       integers. $)
    zlpirlem1 $p |- ( ph -> ( I i^i NN ) =/= (/) ) $=
      ( va cc0 wne cn wcel cfv cz ccnfld zsubrg ax-mp eqid wceq cc adantr cv c0
      cin crg clidl csn csubrg subrgrng a1i c0g cnfld0 subrg0 lidlnz syl3anc wa
      wrex cabs wo cr wss cbs zsscn cnfldbas ressbas2 lidlss sylancr sselda zre
      cneg absor 3syl eleq1 syl5ibrcom cminusg csubg subrgsubg subginv cnfldneg
      simplr zcn eqtr3d simpr lidlnegcl eqeltrrd jaod mpd nnabscl elin sylanbrc
      sylan ne0i syl ex rexlimdva ) AGUAZHIZGBUPZBJUCZUBIZACUDKZBCUELZKZBHUFIWQ
      WTAMNUGLKZWTOMNCDUHPZUIEFGCXABHXAQZXCHCUJLROMNCHDUKULPUMUNAWPWSGBAWOBKZUO
      ZWPWSXGWPUOZWOUQLZWRKZWSXHXIBKZXIJKZXJXHXIWORZXIWOVIZRZURZXKXGXPWPXGWOMKZ
      WOUSKXPABMWOAWTXBBMUTXDEMBXAUDCMSUTMCVALRVBMSCNDVCVDPXEVEVFVGZWOVHWOVJVKT
      XHXMXKXOXHXKXMXFAXFWPVSXIWOBVLVMXHXKXOXNBKZXGXSWPXGWOCVNLZLZXNBXGWONVNLZL
      ZYAXNXGMNVOLKZXQYCYARXCYDOMNVPPXRMNCYBXTWODYBQXTQZVQVFXGXQWOSKYCXNRXRWOVT
      WOVRVKWAXGWTXBXFYABKWTXGXDUIAXBXFETAXFWBCXABXTWOXEYEWCUNWDTXIXNBVLVMWEWFX
      GXQWPXLXRWOWGWJXIBJWHWIWRXIWKWLWMWNWF $.
      $( [3-Jan-2015] $)

    zlpirlem.g $e |- G = sup ( ( I i^i NN ) , RR , `' < ) $.
    $( Lemma for ~ zlpir .  A nonzero ideal of integers contains a least
       positive element. $)
    zlpirlem2 $p |- ( ph -> G e. I ) $=
      ( cn cin cr clt ccnv csup inss1 c1 cuz cfv wss c0 wcel inss2 nnuz sseqtri
      wne zlpirlem1 infmssuzcl sylancr sseldi syl5eqel ) ABCIJZKLMNZCHAUKCULCIO
      AUKPQRZSUKTUEULUKUAUKIUMCIUBUCUDACDEFGUFUKPUGUHUIUJ $.
      $( [3-Jan-2015] $)

    zlpirlem.x $e |- ( ph -> X e. I ) $.
    $( Lemma for ~ zlpir .  All elements of a nonzero ideal of integers are
       divided by the least one. $)
    zlpirlem3 $p |- ( ph -> G || X ) $=
      ( co wceq cn wcel cr cz cfv cc syl2anc syl cdivides wbr cmo cc0 wn wo cle
      clt crp crg clidl wss ccnfld csubrg subrgrng ax-mp a1i cbs zsscn cnfldbas
      zsubrg ressbas2 eqid lidlss sseldd zre cin inss2 ccnv csup c1 cuz c0 nnuz
      sseqtri zlpirlem1 infmssuzcl sylancr syl5eqel sseldi nnrp modlt wb zmodcl
      wne cn0 nn0re nnre ltnle mpbid cdiv cfl cneg cmul caddc cmin nncn redivcl
      nnne0 syl3anc flcl mulcl negsub znegcl mulcom mulneg2 eqtrd oveq2d modval
      wa 3eqtr4rd zlpirlem2 cnfldmul ressmulr lidlmcl syl22anc cnfldadd lidlacl
      ressplusg eqeltrd adantr simpr elin sylanbrc infmssuzle mtand elnn0 sylib
      syl5eqbr orel1 sylc divides3 mpbird ) ABDUAUBZDBUCKZUDLZAYOMNZUEYQYPUFZYP
      AYQBYOUGUBZAYOBUHUBZYSUEZADONZBUINZYTADPNZUUBACPDAEUJNZCEUKQZNZCPULUUEAPU
      MUNQNUUEVAPUMEFUOUPUQZGPCUUFUJEPRULPEURQLUSPREUMFUTVBUPZUUFVCZVDSJVEZDVFT
      ZABMNZUUCACMVGZMBCMVHZABUUNOUHVIVJZUUNIAUUNVKVLQZULZUUNVMWEUUPUUNNUUNMUUQ
      UUOVNVOZACEFGHVPUUNVKVQVRVSVTZBWATZDBWBSAYOONZBONZYTUUAWCAYOWFNZUVBAUUDUU
      MUVDUUKUUTDBWDSZYOWGTAUUMUVCUUTBWHTZYOBWISWJAYQXJZBUUPYOUGIUVGUURYOUUNNZU
      UPYOUGUBUUSUVGYOCNZYQUVHAUVIYQAYODDBWKKZWLQZWMZBWNKZWOKZCADBUVKWNKZWMZWOK
      ZDUVOWPKZUVNYOADRNUVORNZUVQUVRLAPRDUSUUKVTABRNZUVKRNZUVSAUUMUVTUUTBWQTZAP
      RUVKUSAUVJONZUVKPNZAUUBUVCBUDWEZUWCUULUVFAUUMUWEUUTBWSTDBWRWTUVJXATZVTZBU
      VKXBSDUVOXCSAUVMUVPDWOAUVMBUVLWNKZUVPAUVLRNUVTUVMUWHLAPRUVLUSAUWDUVLPNZUW
      FUVKXDTZVTUWBUVLBXESAUVTUWAUWHUVPLUWBUWGBUVKXFSXGXHAUUBUUCYOUVRLUULUVADBX
      ISXKAUUEUUGDCNUVMCNZUVNCNUUHGJAUUEUUGUWIBCNUWKUUHGUWJABCEFGHIXLPEWNUUFCUV
      LBUUJUUIPUMEWNFXMXNXOXPWOEUUFCDUVMUUJPWOUMEFXQXSXRXPXTYAAYQYBYOCMYCYDYOUU
      NVKYEVRYIYFAUVDYRUVEYOYGYHYQYPYJYKAUUMUUDYNYPWCUUTUUKBDYLSYM $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d Z x y z $.
    zlpir.z $e |- Z = ( CCfld |`s ZZ ) $.
    $( The integers are a principal ideal ring but not a field. $)
    zlpir $p |- Z e. LPIR $=
      ( vx vy vz wcel cfv eqid cz ccnfld zsubrg ax-mp cv cc0 wceq cdivides wral
      wa wbr simpr clpir crg clidl clpidl islpir2 csubrg subrgrng cnfld0 subrg0
      wss csn c0g lpi0 a1i eleq1 syl5ibrcom imp wne wrex cn cin ccnv csup simpl
      clt zlpirlem2 wel simpll simplr zlpirlem3 ralrimiva breq1 ralbidv rcla4ev
      cr syl2anc wb eqid1 dvdsrz lpigen adantr mpbird pm2.61dane ssriv mpbir2an
      mpan ) AUAFAUBFZAUCGZAUDGZUJWIAWHWIHZWHHUEIJUFGFZWGKIJABUGLZCWHWICMZWHFZW
      MWIFZWMNUKZWNWMWPOZWOWNWOWQWPWIFZWRWNWGWRWLWIANWJWKNAULGOKIJANBUHUILUMLUN
      WMWPWIUOUPUQWNWMWPURZRZWODMZEMZPSZEWMQZDWMUSZWTWMUTVAVOVEVBVCZWMFXFXBPSZE
      WMQZXEWTXFWMABWNWSVDWNWSTXFHZVFWTXGEWMWTECVGZRXFWMXBABWNWSXJVHWNWSXJVIXIW
      TXJTVJVKXDXHDXFWMXAXFOXCXGEWMXAXFXBPVLVMVNVPWNWOXEVQZWSWGWNXKWLDEPWIAWHWM
      WHVRWIVRABVSVTWFWAWBWCWDWE $.
      $( [3-Jan-2015] $)
  $}

