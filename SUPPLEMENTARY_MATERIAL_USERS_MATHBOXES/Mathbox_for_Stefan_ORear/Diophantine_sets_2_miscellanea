$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Diophantine sets 2 miscellanea
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( If two functions agree on their common domain, express their union as a
     union of three functions with pairwise disjoint domains.  (Contributed by
     Stefan O'Rear, 9-Oct-2014.) $)
  resasplit $p |- ( ( F Fn A /\ G Fn B /\ ( F |` ( A i^i B ) ) = ( G |` ( A i^i
      B ) ) ) -> ( F u. G ) = ( ( F |` ( A i^i B ) ) u. ( ( F |` ( A \ B ) ) u.
      ( G |` ( B \ A ) ) ) ) ) $=
    ( wfn cin cres w3a cun fnresdm uneq12 syl2an inundif reseq2i resundi eqtr3i
    wceq cdif uneq1i syl6eq 3adant3 simp3 uneq1d uneq2d incom eqtri uneq12i un4
    syl6reqr unidm eqtr3d ) CAEZDBEZCABFZGZDUNGZQZHZCAGZDBGZIZCDIZUOCABRZGZDBAR
    ZGZIZIZULUMVAVBQZUQULUSCQUTDQVIUMACJBDJUSCUTDKLUAURVAUOUOIZVGIZVHURVAUOVDIZ
    UOVFIZIZVKURVNVLUPVFIZIVAURVMVOVLURUOUPVFULUMUQUBUCUDUSVLUTVOCUNVCIZGUSVLVP
    ACABMNCUNVCOPDUNVEIZGUTVOVQBDVQBAFZVEIBUNVRVEABUESBAMUFNDUNVEOPUGUIUOVDUOVF
    UHTVJUOVGUOUJSTUK $.
    $( [9-Oct-2014] $)

  $( The union of two functions which agree on their common domain is a
     function.  (Contributed by Stefan O'Rear, 9-Oct-2014.) $)
  fresaun $p |- ( ( F : A --> C /\ G : B --> C /\ ( F |` ( A i^i B ) ) = ( G |`
      ( A i^i B ) ) ) -> ( F u. G ) : ( A u. B ) --> C ) $=
    ( wf cin cres wceq cun c0 wss fssres sylancl difss disjdif 3eqtri a1i eqtri
    cdif w3a simp1 inss1 simp2 indifdir difeq1i 0dif syl21anc indi inass ineq2i
    fun in0 incom ineq1i uneq12i un0 wfn ffn resasplit syl3an feq1d un12 uneq1i
    id inundif uneq2i undif1 unidm feq23i syl6rbbr mpbid ) ACDFZBCEFZDABGZHZEVO
    HIZUAZVOABTZBATZJZJZCCCJZJZVPDVSHZEVTHZJZJZFZABJZCDEJZFZVRVOCVPFZWAWCWGFZVO
    WAGZKIZWIVRVMVOALWMVMVNVQUBZABUCACVODMNVRVSCWEFZVTCWFFZVSVTGZKIZWNVRVMVSALW
    RWQABOACVSDMNVRVNVTBLWSVMVNVQUDBAOBCVTEMNXAVRWTAVTGZBVTGZTKXCTKABVTUEXBKXCA
    BPZUFXCUGQRVSVTCCWEWFULUHWPVRWOVOVSGZVOVTGZJKKJKVOVSVTUIXEKXFKXEABVSGZGAKGK
    ABVSUJXGKABAPUKAUMQXFBAGZVTGZKVOXHVTABUNZUOXIBXBGBKGKBAVTUJXBKBXDUKBUMQSUPK
    UQQRVOWACWCVPWGULUHVRWLWJCWHFWIVRWJCWKWHVMDAURVNEBURVQVQWKWHIACDUSBCEUSVQVE
    ABDEUTVAVBWBWDWJCWHWBVSVOVTJZJVSBJWJVOVSVTVCXKBVSXKXHVTJBVOXHVTXJVDBAVFSVGA
    BVHQWDWCCWCCCCVIZVGXLSVJVKVL $.
    $( [9-Oct-2014] $)

  $( From the union of two functions that agree on the domain overlap, either
     component can be recovered by restriction.  (Contributed by Stefan O'Rear,
     9-Oct-2014.) $)
  fresaunres2 $p |- ( ( F : A --> C /\ G : B --> C /\ ( F |` ( A i^i B ) ) = (
      G |` ( A i^i B ) ) ) -> ( ( F u. G ) |` B ) = G ) $=
    ( wf cin cres wceq cun cdif wfn ffn resundir wss ax-mp c0 cdm eqtri syl5eq
    w3a id resasplit syl3an reseq1d inss2 resabs2 uneq12i ineq2i disjdif ineq1i
    dmres inass inss1 0ss eqssi 3eqtr3i wrel wb relres reldm0 mpbir difss simp3
    uneq2i uneq1d wa uncom un0 resundi incom uneq1i inundif reseq2i fnresdm syl
    adantl syl5eqr 3adant3 eqtrd ) ACDFZBCEFZDABGZHZEWCHZIZUAZDEJZBHWDDABKZHZEB
    AKZHZJZJZBHZEWGWHWNBWADALWBEBLZWFWFWHWNIACDMBCEMZWFUBABDEUCUDUEWGWOWDBHZWMB
    HZJZEWDWMBNWGWTWDWJBHZWLBHZJZJZEWRWDWSXCWCBOWRWDIABUFDWCBUGPWJWLBNUHWGXDWDQ
    WLJZJZEXCXEWDXAQXBWLXAQIZXARZQIZXHBWJRZGZQWJBULXKBWIDRZGZGZQXJXMBDWIULUIBWI
    GZXLGQXLGZXNQXOQXLBAUJUKBWIXLUMXPQQXLUNXPUOUPUQSSXAURXGXIUSWJBUTXAVAPVBWKBO
    XBWLIBAVCEWKBUGPUHVEWGXFWEXEJZEWGWDWEXEWAWBWFVDVFWAWBXQEIWFWAWBVGZXQWEWLJZE
    XEWLWEXEWLQJWLQWLVHWLVISVEXRXSEWCWKJZHZEEWCWKVJXRYAEBHZEXTBEXTBAGZWKJBWCYCW
    KABVKVLBAVMSVNWBYBEIZWAWBWPYDWQBEVOVPVQTVRTVSVTTTTVT $.
    $( [9-Oct-2014] $)

  ${
    $d N a $.  $d A a $.  $d B a $.
    $( Membership in a set of lower integers.  (Contributed by Stefan O'Rear,
       9-Oct-2014.) $)
    ellz1 $p |- ( B e. ZZ -> ( A e. ( ZZ \ ( ZZ>= ` ( B + 1 ) ) ) <-> ( A e. ZZ
        /\ A <_ B ) ) ) $=
      ( cz c1 caddc co cuz cfv cdif wcel wn wa cle wbr eldif clt notbid cr zre
      wb zltp1le lenlt syl2anr peano2z eluz sylan 3bitr4rd pm5.32da syl5bb ) AC
      BDEFZGHZIJACJZAUKJZKZLBCJZULABMNZLACUKOUOULUNUPUOULLZBAPNZKZUJAMNZKUPUNUQ
      URUTBAUAQULARJBRJUPUSTUOASBSABUBUCUQUMUTUOUJCJULUMUTTBUDUJAUEUFQUGUHUI $.
      $( [9-Oct-2014] $)

    $( A set of lower integers and upper integers which abut or overlap is all
       of the integers.  (Contributed by Stefan O'Rear, 9-Oct-2014.) $)
    lzunuz $p |- ( ( A e. ZZ /\ B e. ZZ /\ B <_ ( A + 1 ) ) -> ( ( ZZ \ ( ZZ>=
        ` ( A + 1 ) ) ) u. ( ZZ>= ` B ) ) = ZZ ) $=
      ( va cz wcel c1 caddc co cle wbr w3a cuz cfv wo wa wb cr zre syl ex ellz1
      cdif cun cv 3ad2ant1 eluz1 3ad2ant2 orbi12d wi clt adantl simpl1 lelttric
      elun syl2anc simpll2 simpll1 peano2z 3syl ad2antlr simpll3 zltp1le biimpa
      3ad2antl1 letrd orim2d mpd pm4.71 sylib andi syl6rbb bitrd syl5bb eqrdv )
      ADEZBDEZBAFGHZIJZKZCDVQLMUBZBLMZUCZDCUDZWBEWCVTEZWCWAEZNZVSWCDEZWCVTWAUNV
      SWFWGWCAIJZOZWGBWCIJZOZNZWGVSWDWIWEWKVOVPWDWIPVRWCAUAUEVPVOWEWKPVRBWCUFUG
      UHVSWGWGWHWJNZOZWLVSWGWMUIWGWNPVSWGWMVSWGOZWHAWCUJJZNZWMWOWCQEZAQEZWQWGWR
      VSWCRZUKWOVOWSVOVPVRWGULARSWCAUMUOWOWPWJWHWOWPWJWOWPOZBVQWCXAVPBQEVOVPVRW
      GWPUPBRSXAVOVQDEVQQEVOVPVRWGWPUQAURVQRUSWGWRVSWPWTUTVOVPVRWGWPVAWOWPVQWCI
      JZVOVPWGWPXBPVRAWCVBVDVCVETVFVGTWGWMVHVIWGWHWJVJVKVLVMVN $.
      $( [9-Oct-2014] $)

    $( Express a one-based finite range as the intersection of lower integers
       with ` NN ` .  (Contributed by Stefan O'Rear, 9-Oct-2014.) $)
    fz1eqin $p |- ( N e. NN0 -> ( 1 ... N ) = ( ( ZZ \ ( ZZ>= ` ( N + 1 ) ) )
        i^i NN ) ) $=
      ( va cn0 wcel c1 cfz co cz caddc cuz cfv cdif cn cin cv cle wbr wa w3a wb
      1z nn0z elfz1 sylancr 3anass ancom anbi2i anandi 3bitri syl6bb elin ellz1
      syl elnnz1 a1i anbi12d syl5bb bitr4d eqrdv ) ACDZBEAFGZHAEIGJKLZMNZUTBOZV
      ADZVDHDZVDAPQZRZVFEVDPQZRZRZVDVCDZUTVEVFVIVGSZVKUTEHDAHDZVEVMTUAAUBZVDEAU
      CUDVMVFVIVGRZRVFVGVIRZRVKVFVIVGUEVPVQVFVIVGUFUGVFVGVIUHUIUJVLVDVBDZVDMDZR
      UTVKVDVBMUKUTVRVHVSVJUTVNVRVHTVOVDAULUMVSVJTUTVDUNUOUPUQURUS $.
      $( [9-Oct-2014] $)
  $}

  ${
    $d N a b c $.
    $( Lower integers are countably infinite.  (Contributed by Stefan O'Rear,
       10-Oct-2014.) $)
    lzenom $p |- ( N e. ZZ -> ( ZZ \ ( ZZ>= ` ( N + 1 ) ) ) ~~ om ) $=
      ( cz wcel c1 co cn cen wbr cmin cvv cle wa wceq syl2anc cc cr wb ad2antrl
      sseldi anbi12d va vb caddc cuz cfv cdif com cv zex difexg ax-mp ovex a1ii
      a1i simpl peano2zdi simprl zsubcl simprr zcn adantr ax-1cn pncan breqtrrd
      sylancl zre zssre 1re lesub syl3anc mpbid zsscn nncan jca31 adantrr eleq1
      eqcomd breq2 oveq2 eqeq2d mpbird recnd pncan2 eqbrtrd suble breq1 impbida
      ad2antll ellz1 anbi1d elnnz1 3bitr4d en2d nnenom entr ) ABCZBADUCEZUDUEZU
      FZFGHFUGGHWSUGGHWPUAUBWSFWQUAUHZIEZWQUBUHZIEZWSJCZWPBJCXDUIBWRJUJUKUNWPWT
      WSCZXAJCWQWTIULUMWPXBFCZXCJCWQXBIULUMWPWTBCZWTAKHZLZXBXAMZLZXBBCZDXBKHZLZ
      WTXCMZLZXEXJLXFXOLWPXKXPWPXKLXPXABCZDXAKHZLZWTWQXAIEZMZLZWPXIYBXJWPXILZXQ
      XRYAYCWQBCZXGXQYCAWPXIUOUPZWPXGXHUQWQWTURNYCWTWQDIEZKHZXRYCWTAYFKWPXGXHUS
      YCAOCZDOCZYFAMWPYHXIAUTVAVBADVCVEVDYCWTPCZWQPCZDPCZYGXRQXGYJWPXHWTVFRYCBP
      WQVGYESYLYCVHUNWTWQDVIVJVKYCXTWTYCWQOCZWTOCZXTWTMYCBOWQVLYESXGYNWPXHWTUTR
      WQWTVMNVQVNVOXJXPYBQWPXIXJXNXSXOYAXJXLXQXMXRXBXABVPXBXADKVRTXJXCXTWTXBXAW
      QIVSVTTWHWAWPXPLXKXCBCZXCAKHZLZXBWQXCIEZMZLZWPXNYTXOWPXNLZYOYPYSUUAYDXLYO
      UUAAWPXNUOUPZWPXLXMUQWQXBURNUUAWQAIEZXBKHZYPUUAUUCDXBKUUAYHYIUUCDMUUAAWPA
      PCZXNAVFVAZWBVBADWCVEWPXLXMUSWDUUAYKUUEXBPCZUUDYPQUUABPWQVGUUBSUUFXLUUGWP
      XMXBVFRWQAXBWEVJVKUUAYRXBUUAYMXBOCZYRXBMUUABOWQVLUUBSXLUUHWPXMXBUTRWQXBVM
      NVQVNVOXOXKYTQWPXNXOXIYQXJYSXOXGYOXHYPWTXCBVPWTXCAKWFTXOXAYRXBWTXCWQIVSVT
      TWHWAWGWPXEXIXJWTAWIWJWPXFXNXOXFXNQWPXBWKUNWJWLWMWNWSFUGWOVE $.
      $( [10-Oct-2014] $)
  $}

  ${
    elmapresaun.1 $e |- A e. _V $.
    elmapresaun.2 $e |- B e. _V $.
    $( ~ fresaun transposed to mappings.  (Contributed by Stefan O'Rear,
       9-Oct-2014.) $)
    elmapresaun $p |- ( ( F e. ( C ^m A ) /\ G e. ( C ^m B ) /\ ( F |` ( A i^i
        B ) ) = ( G |` ( A i^i B ) ) ) -> ( F u. G ) e. ( C ^m ( A u. B ) ) )
        $=
      ( cmap co wcel cin cres cun wf cvv wb elmapex1 elmapg sylancl ibi wceq id
      w3a fresaun syl3an unex 3ad2ant1 mpbird ) DCAHIJZECBHIJZDABKZLEUKLUAZUCDE
      MZCABMZHIJZUNCUMNZUIACDNZUJBCENZULULUPUIUQUICOJZAOJUIUQPDCAQZFCADOORSTUJU
      RUJUSBOJUJURPECBQGCBEOORSTULUBABCDEUDUEUIUJUOUPPZULUIUSUNOJVAUTABFGUFCUNU
      MOORSUGUH $.
      $( [9-Oct-2014] $)

    $( ~ fresaunres2 transposed to mappings.  (Contributed by Stefan O'Rear,
       9-Oct-2014.) $)
    elmapresaunres2 $p |- ( ( F e. ( C ^m A ) /\ G e. ( C ^m B ) /\ ( F |` ( A
        i^i B ) ) = ( G |` ( A i^i B ) ) ) -> ( ( F u. G ) |` B ) = G ) $=
      ( cmap co wcel wf cin cres wceq cvv wb elmapex1 elmapg sylancl ibi cun id
      fresaunres2 syl3an ) DCAHIJZACDKZECBHIJZBCEKZDABLZMEUIMNZUJDEUABMENUEUFUE
      COJZAOJUEUFPDCAQFCADOORSTUGUHUGUKBOJUGUHPECBQGCBEOORSTUJUBABCDEUCUD $.
      $( [9-Oct-2014] $)
  $}

