$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Additional elementary logic and set theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d ph z w $.  $d ps z w $.  $d x y z w $.
    $( The difference of two ordered-pair abstractions.  (Contributed by Stefan
       O'Rear, 17-Jan-2015.) $)
    difopab $p |- ( { <. x , y >. | ph } \ { <. x , y >. | ps } ) =
        { <. x , y >. | ( ph /\ -. ps ) } $=
      ( vz vw copab cdif wn wa wrel relopab cv wcel sban sbbii bitri opelopabsb
      wsb sbn reldif ax-mp cop anbi2i notbii anbi12i 3bitr4ri 3bitr4i eqrelriiv
      eldif ) EFACDGZBCDGZHZABIZJZCDGZUKKUMKACDLUKULUAUBUOCDLEMFMUCZUKNZUQULNZI
      ZJZUOCESZDFSZUQUMNUQUPNACESZUNCESZJZDFSZVDDFSZBCESZDFSZIZJZVCVAVGVHVEDFSZ
      JVLVDVEDFOVMVKVHVMVIIZDFSVKVEVNDFBCETPVIDFTQUDQVBVFDFAUNCEOPURVHUTVKACDEF
      RUSVJBCDEFRUEUFUGUQUKULUJUOCDEFRUHUI $.
      $( [17-Jan-2015] $)
  $}

  $( Membership of the original in a power set.  (Contributed by Stefan O'Rear,
     1-Feb-2015.) $)
  pwidg $p |- ( A e. V -> A e. ~P A ) $=
    ( wcel cpw wss ssid elpwg mpbiri ) ABCAADCAAEAFAABGH $.
    $( [1-Feb-2015] $)

  $( Abstraction restricted to a union.  (Contributed by Stefan O'Rear,
     5-Feb-2015.) $)
  rabun2 $p |- { x e. ( A u. B ) | ph } =
      ( { x e. A | ph } u. { x e. B | ph } ) $=
    ( cv cun wcel wa cab crab elun anbi1i andir bitri abbii unab eqtr4i uneq12i
    wo df-rab 3eqtr4i ) BEZCDFZGZAHZBIZUBCGZAHZBIZUBDGZAHZBIZFZABUCJABCJZABDJZF
    UFUHUKSZBIUMUEUPBUEUGUJSZAHUPUDUQAUBCDKLUGUJAMNOUHUKBPQABUCTUNUIUOULABCTABD
    TRUA $.
    $( [5-Feb-2015] $)

  $( Demonstrate by witnesses that two classes lack a subclass relation.
     (Contributed by Stefan O'Rear, 5-Feb-2015.) $)
  nelss $p |- ( ( A e. B /\ -. A e. C ) -> -. B C_ C ) $=
    ( wcel wn wss ssel com12 con3d imp ) ABDZACDZEBCFZEKMLMKLBCAGHIJ $.
    $( [5-Feb-2015] $)

