$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Distributive lattices
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    latmass.b $e |- B = ( Base ` K ) $.
    latmass.m $e |- M = ( meet ` K ) $.
    $( Lattice meet is associative.  (Contributed by Stefan O'Rear,
       30-Jan-2015.) $)
    latmass $p |- ( ( K e. Lat /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
        ( ( X M Y ) M Z ) = ( X M ( Y M Z ) ) ) $=
      ( clat wcel codu cfv w3a co wceq eqid odulat odubas odujoin latjass sylan
      ) BIJBKLZIJDAJEAJFAJMDECNFCNDEFCNCNOUBBUBPZQACUBDEFAUBBUCGRUBCBUCHSTUA $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d u v w x y z K $.  $d u v w x y z B $.  $d u v w x y z J $.
    $d u v w x y z M $.
    latdisd.b $e |- B = ( Base ` K ) $.
    latdisd.j $e |- J = ( join ` K ) $.
    latdisd.m $e |- M = ( meet ` K ) $.
    $( Lemma for ~ latdisd . $)
    latdisdlem $p |- ( K e. Lat ->
        ( A. u e. B A. v e. B A. w e. B ( u J ( v M w ) ) =
          ( ( u J v ) M ( u J w ) ) ->
        A. x e. B A. y e. B A. z e. B ( x M ( y J z ) ) =
          ( ( x M y ) J ( x M z ) ) ) ) $=
      ( wcel cv co wceq syl3anc oveq1 oveq2d clat wa w3a wi simpl simpr1 simpr2
      wral latmcl simpr3 oveq12d eqeq12d weq oveq1d rcla43v imp latjcom latabs1
      oveq2 eqtrd adantr latjcl latmass syl13anc eqtr3d exp31 com23 ralrimivvva
      latabs2 3eqtrrd imp31 ex ) IUANZFOZEOZDOZJPZHPZVNVOHPZVNVPHPZJPZQZDGUHEGU
      HFGUHZAOZBOZCOZHPZJPZWDWEJPZWDWFJPZHPZQZCGUHBGUHAGUHVMWCUBWLABCGGGVMWCWDG
      NZWEGNZWFGNZUCZWLVMWPWCWLVMWPWCWLVMWPUBZWCUBZWKWIWDHPZWIWFHPZJPZWDWFWIHPZ
      JPZWHWQWCWKXAQZWQWIGNZWMWOWCXDUDWQVMWMWNXEVMWPUEZVMWMWNWOUFZVMWMWNWOUGZGI
      JWDWEKMUIRZXGVMWMWNWOUJZWBXDWIVQHPZWIVOHPZWIVPHPZJPZQWIWDVPJPZHPZWSXMJPZQ
      FEDWIWDWFGGGVNWIQZVRXKWAXNVNWIVQHSXRVSXLVTXMJVNWIVOHSVNWIVPHSUKULEAUMZXKX
      PXNXQXSVQXOWIHVOWDVPJSZTXSXLWSXMJVOWDWIHUSUNULDCUMZXPWKXQXAYAXOWJWIHVPWFW
      DJUSTYAXMWTWSJVPWFWIHUSTULUORUPWQXAXCQWCWQWSWDWTXBJWQWSWDWIHPZWDWQVMXEWMW
      SYBQXFXIXGGHIWIWDKLUQRWQVMWMWNYBWDQXFXGXHGHIJWDWEKLMURRUTWQVMXEWOWTXBQXFX
      IXJGHIWIWFKLUQRUKVAWRXCWDWFWDHPZWFWEHPZJPZJPZWHWRXBYEWDJWQWCXBYEQZWQWOWMW
      NWCYGUDXJXGXHWBYGWFVQHPZWFVOHPZWFVPHPZJPZQWFXOHPZYCYJJPZQFEDWFWDWEGGGFCUM
      ZVRYHWAYKVNWFVQHSYNVSYIVTYJJVNWFVOHSVNWFVPHSUKULXSYHYLYKYMXSVQXOWFHXTTXSY
      IYCYJJVOWDWFHUSUNULDBUMZYLXBYMYEYOXOWIWFHVPWEWDJUSTYOYJYDYCJVPWEWFHUSTULU
      ORUPTWQYFWHQWCWQWDYCJPZYDJPZYFWHWQVMWMYCGNZYDGNZYQYFQXFXGWQVMWOWMYRXFXJXG
      GHIWFWDKLVBRWQVMWOWNYSXFXJXHGHIWFWEKLVBRGIJWDYCYDKMVCVDWQYPWDYDWGJWQYPWDW
      DWFHPZJPZWDWQYCYTWDJWQVMWOWMYCYTQXFXJXGGHIWFWDKLUQRTWQVMWMWOUUAWDQXFXGXJG
      HIJWDWFKLMVIRUTWQVMWOWNYDWGQXFXJXHGHIWFWEKLUQRUKVEVAUTVJVFVGVKVHVL $.
      $( [30-Jan-2015] $)

    $( In a lattice, joins distribute over meets if and only if meets
       distribute over joins; the distributive property is self-dual.
       (Contributed by Stefan O'Rear, 29-Jan-2015.) $)
    latdisd $p |- ( K e. Lat ->
        ( A. x e. B A. y e. B A. z e. B ( x J ( y M z ) ) =
          ( ( x J y ) M ( x J z ) ) <->
        A. x e. B A. y e. B A. z e. B ( x M ( y J z ) ) =
          ( ( x M y ) J ( x M z ) ) ) ) $=
      ( vu vv vw cv co wceq wral weq oveq1 eqeq12d clat wcel latdisdlem codu wi
      cfv eqid odulat odubas odujoin odumeet impbid oveq12d oveq2d oveq2 oveq1d
      syl cbvral3v syl6bb ) FUAUBZANZBNZCNZGOEOVAVBEOVAVCEOGOPCDQBDQADQZKNZLNZM
      NZEOZGOZVEVFGOZVEVGGOZEOZPZMDQLDQKDQZVAVBVCEOZGOZVAVBGOZVAVCGOZEOZPZCDQBD
      QADQUTVDVNKLMCBADEFGHIJUCUTFUDUFZUAUBVNVDUEWAFWAUGZUHABCMLKDGWAEDWAFWBHUI
      WAGFWBJUJWAEFWBIUKUCUQULVMVTVAVHGOZVAVFGOZVAVGGOZEOZPVAVBVGEOZGOZVQWEEOZP
      KLMABCDDDKARZVIWCVLWFVEVAVHGSWJVJWDVKWEEVEVAVFGSVEVAVGGSUMTLBRZWCWHWFWIWK
      VHWGVAGVFVBVGESUNWKWDVQWEEVFVBVAGUOUPTMCRZWHVPWIVSWLWGVOVAGVGVCVBEUOUNWLW
      EVRVQEVGVCVAGUOUNTURUS $.
      $( [29-Jan-2015] $)
  $}

  $c DLat $.

  $( The class of distributive lattices. $)
  cdlat $a class DLat $.

  ${
    $d k b j m x y z $.
    $( A _distributive lattice_ is a lattice in which meets distribute over
       joins, or equivalently ( ~ latdisd ) joins distribute over meets. $)
    df-dlat $a |- DLat = { k e. Lat | [ ( Base ` k ) / b ]
        [ ( join ` k ) / j ] [ ( meet ` k ) / m ] A. x e. b A. y e. b
          A. z e. b ( x m ( y j z ) ) = ( ( x m y ) j ( x m z ) ) } $.
  $}

  ${
    $d k b j m x y z K $.  $d k b j m x y z B $.  $d k b j m x y z J $.
    $d k b j m x y z M $.
    isdlat.b $e |- B = ( Base ` K ) $.
    isdlat.j $e |- J = ( join ` K ) $.
    isdlat.m $e |- M = ( meet ` K ) $.
    $( Property of being a distributive lattice.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    isdlat $p |- ( K e. DLat <-> ( K e. Lat /\ A. x e. B A. y e. B A. z e. B
        ( x M ( y J z ) ) = ( ( x M y ) J ( x M z ) ) ) ) $=
      ( vj vm vb cv co wceq wral cfv wsbc wb vk cmee cjn cbs clat cdlat syl6eqr
      fveq2 dfsbcq syl sbcbidv bitrd cvv fvex eqeltri wa raleq raleqbi1dv simpr
      eqidd simpl oveqd oveq123d eqeq12d ralbidv 2ralbidv sylan9bb 3impb sbc3ie
      a1i 3bitrd df-dlat elrab2 ) ANZBNZCNZKNZOZLNZOZVNVOVSOZVNVPVSOZVQOZPZCMNZ
      QZBWEQZAWEQZLUANZUBRZSZKWIUCRZSZMWIUDRZSZVNVOVPEOZGOZVNVOGOZVNVPGOZEOZPZC
      DQZBDQADQZUAFUEUFWIFPZWOWMMDSZWHLGSZKESZMDSZXCXDWNDPWOXETXDWNFUDRZDWIFUDU
      HHUGWMMWNDUIUJXDWMXGMDXDWMWKKESZXGXDWLEPWMXJTXDWLFUCRZEWIFUCUHIUGWKKWLEUI
      UJXDWKXFKEXDWJGPWKXFTXDWJFUBRZGWIFUBUHJUGWHLWJGUIUJUKULUKXHXCTXDWHXCMKLDE
      GDXIUMHFUDUNUOEXKUMIFUCUNUOGXLUMJFUBUNUOWEDPZVQEPZVSGPZWHXCTXMWHWDCDQZBDQ
      ZADQXNXOUPZXCWGXQAWEDWFXPBWEDWDCWEDUQURURXRXPXBABDDXRWDXACDXRVTWQWCWTXRVN
      VNVRWPVSGXNXOUSZXRVNUTXRVQEVOVPXNXOVAZVBVCXRWAWRWBWSVQEXTXRVSGVNVOXSVBXRV
      SGVNVPXSVBVCVDVEVFVGVHVIVJVKABCKUALMVLVM $.
      $( [30-Jan-2015] $)

    $d X x y z $.  $d Y x y z $.  $d Z x y z $.

    $( In a distributive lattice, meets distribute over joins.  (Contributed by
       Stefan O'Rear, 30-Jan-2015.) $)
    dlatmjdi $p |- ( ( K e. DLat /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
        ( X M ( Y J Z ) ) = ( ( X M Y ) J ( X M Z ) ) ) $=
      ( vx vy vz wcel cv co wceq wral oveq1 eqeq12d clat isdlat simprbi oveq12d
      cdlat w3a oveq2d oveq2 oveq1d rcla43v mpan9 ) CUENZKOZLOZMOZBPZDPZUMUNDPZ
      UMUODPZBPZQZMARLARKARZEANFANGANUFEFGBPZDPZEFDPZEGDPZBPZQZULCUANVBKLMABCDH
      IJUBUCVAVHEUPDPZEUNDPZEUODPZBPZQEFUOBPZDPZVEVKBPZQKLMEFGAAAUMEQZUQVIUTVLU
      MEUPDSVPURVJUSVKBUMEUNDSUMEUODSUDTUNFQZVIVNVLVOVQUPVMEDUNFUOBSUGVQVJVEVKB
      UNFEDUHUITUOGQZVNVDVOVGVRVMVCEDUOGFBUHUGVRVKVFVEBUOGEDUHUGTUJUK $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d K x y z $.
    $( A distributive lattice is a lattice.  (Contributed by Stefan O'Rear,
       30-Jan-2015.) $)
    dlatl $p |- ( K e. DLat -> K e. Lat ) $=
      ( vx vy vz cdlat wcel clat cjn cfv cmee wceq cbs wral eqid isdlat simplbi
      cv co ) AEFAGFBQZCQZDQZAHIZRAJIZRSTUCRSUAUCRUBRKDALIZMCUDMBUDMBCDUDUBAUCU
      DNUBNUCNOP $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d K x y z $.  $d D x y z $.  $d V x y z $.
    odudlat.d $e |- D = ( ODual ` K ) $.
    $( The dual of a distributive lattice is a distributive lattice and
       conversely.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    odudlatb $p |- ( K e. V -> ( K e. DLat <-> D e. DLat ) ) $=
      ( vx vy vz wcel clat cv cjn cfv co cmee wceq wral wa cdlat eqid isdlat
      cbs latdisd bicomd pm5.32i odulatb anbi1d syl5bb odujoin odumeet 3bitr4g
      odubas ) BCHZBIHZEJZFJZGJZBKLZMBNLZMUNUOURMUNUPURMUQMOGBUALZPFUSPEUSPZQZA
      IHZUNUOUPURMUQMUNUOUQMUNUPUQMURMOGUSPFUSPEUSPZQZBRHARHVAUMVCQULVDUMUTVCUM
      VCUTEFGUSUQBURUSSZUQSZURSZUBUCUDULUMVBVCABCDUEUFUGEFGUSUQBURVEVFVGTEFGUSU
      RAUQUSABDVEUKAURBDVGUHAUQBDVFUITUJ $.
      $( [30-Jan-2015] $)
  $}

  ${
    dlatjmdi.b $e |- B = ( Base ` K ) $.
    dlatjmdi.j $e |- J = ( join ` K ) $.
    dlatjmdi.m $e |- M = ( meet ` K ) $.
    $( In a distributive lattice, joins distribute over meets.  (Contributed by
       Stefan O'Rear, 30-Jan-2015.) $)
    dlatjmdi $p |- ( ( K e. DLat /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
        ( X J ( Y M Z ) ) = ( ( X J Y ) M ( X J Z ) ) ) $=
      ( cdlat wcel codu cfv w3a co wceq eqid odudlatb ibi odujoin odumeet sylan
      odubas dlatmjdi ) CKLZCMNZKLZEALFALGALOEFGDPBPEFBPEGBPDPQUFUHUGCKUGRZSTAD
      UGBEFGAUGCUIHUDUGDCUIJUAUGBCUIIUBUEUC $.
      $( [30-Jan-2015] $)
  $}

