$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Liouville's approximation theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F a $.  $d X a $.  $d Y a $.  $d ph a $.
    aalioulem1.a $e |- ( ph -> F e. ( Poly ` ZZ ) ) $.
    aalioulem1.b $e |- ( ph -> X e. ZZ ) $.
    aalioulem1.c $e |- ( ph -> Y e. NN ) $.
    $( Lemma for ~ aaliou .  An integer polynomial cannot inflate the
       denominator of a rational by more than its degree. $)
    aalioulem1 $p |- ( ph -> ( ( F ` ( X / Y ) ) x. ( Y ^ ( deg ` F ) ) )
        e. ZZ ) $=
      ( va co cfv cexp cmul cc0 cz wcel cc wceq syl syl2anc cn0 cdiv cfz cv csu
      cdgr ccoe cply wne zcn nncn nnne0 divcl syl3anc coeid2 oveq1d fzfid dgrcl
      cn expcl wa zsscn wf 0z coef2 sylancl elfznn0 ffvelrn syl2an sseldi mulcl
      eqid fsummulc1 eqtrd adantr mulass adantl expdiv syl121anc nnexpcl nn0ssz
      div13 cmin elfzelz expsub syl22anc nnz fvex fznn0sub mpan zexpcl eqeltrrd
      cvv zmulcl eqeltrd fsumzcl ) ACDUAIZBJZDBUEJZKIZLIZMWRUBIZHUCZBUFJZJZWPXB
      KIZLIZWSLIZHUDZNAWTXAXFHUDZWSLIXHAWQXIWSLABNUGJOZWPPOZWQXIQEACPOZDPOZDMUH
      ZXKACNOZXLFCUIRADUROZXMGDUJRZAXPXNGDUKRZCDULUMZXCNHBWRWPXCVKZWRVKUNSUOAXA
      XFWSHAMWRUPZAXMWRTOZWSPOZXQAXJYBENBUQRZDWRUSZSAXBXAOZUTZXDPOZXEPOZXFPOYGN
      PXDVAATNXCVBZXBTOZXDNOZYFAXJMNOYJEVCXCNBXTVDVEXBWRVFZTNXBXCVGVHZVIZAXKYKY
      IYFXSYMWPXBUSVHZXDXEVJSVLVMAXAXGHYAYGXGXDXEWSLIZLIZNYGYHYIYCXGYRQYOYPYGXM
      YBYCAXMYFXQVNZAYBYFYDVNZYESZXDXEWSVOUMYGYLYQNOYRNOYNYGYQWSDXBKIZUAIZCXBKI
      ZLIZNYGYQUUDUUBUAIZWSLIZUUEYGXEUUFWSLYGXLXMXNYKXEUUFQYGNPCVAAXOYFFVNVIZYS
      AXNYFXRVNZYFYKAYMVPZCDXBVQVRUOYGUUDPOZUUBPOZUUBMUHZYCUUGUUEQYGXLYKUUKUUHU
      UJCXBUSSYGUUBUROZUULAXPYKUUNYFGYMDXBVSVHZUUBUJRYGUUNUUMUUOUUBUKRUUAUUDUUB
      WSWAVRVMYGUUCNOUUDNOZUUENOYGDWRXBWBIZKIZUUCNYGXMXNWRNOXBNOZUURUUCQYSUUIYG
      TNWRVTYTVIYFUUSAXBMWRWCVPDWRXBWDWEYGDNOZUUQTOZUURNOYGXPUUTAXPYFGVNDWFRYFU
      VAAWRWLOYFUVABUEWGWLXBMWRWHWIVPDUUQWJSWKAXOYKUUPYFFYMCXBWJVHUUCUUDWMSWNXD
      YQWMSWNWOWN $.
      $( [12-Nov-2014] $)
  $}

  ${
    $d ph x p q r a b c d $.  $d A x p q r a b c d $.  $d F x p q r a b c d $.
    aalioulem2.a $e |- N = ( deg ` F ) $.
    aalioulem2.b $e |- ( ph -> F e. ( Poly ` ZZ ) ) $.
    aalioulem2.c $e |- ( ph -> N e. NN ) $.
    aalioulem2.d $e |- ( ph -> A e. RR ) $.
    $( Lemma for ~ aaliou . $)
    aalioulem2 $p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN (
        ( F ` ( p / q ) ) = 0 -> ( A = ( p / q ) \/
          ( x / ( q ^ N ) ) <_ ( abs ` ( A - ( p / q ) ) ) ) ) ) $=
      ( cc0 wceq cle crp cr c1 wcel wa syl vr va vb vc vd cv cdiv cfv cmin cabs
      co wbr wo wi cn wral cz wrex cexp csn ccnv cima crab cun clt csup wss 1rp
      snssi ax-mp ssrab2 unssi wor cfn c0 wne ltso cnvso mpbi a1i snfi cab cdgr
      chash cply c0p nnne0 eqcomi dgr0 3netr4g necon3i eqid fta1 syl2anc simpld
      fveq2 abrexfi cin dfrab2 inss1 eqsstri ssfi sylancl unfi sylancr 1re snid
      elexi elun1 ne0i mp2b rpssre sstri fisup2g syl13anc sseldi wn rpge0 breq1
      0re ralbidv rcla4ev reximi cc ad2antrr simplr resubcl recnd wb 3syl oveq2
      cvv fveq2d syl3anc ex rpre adantr elrp sylib ralimdva mp2an ssralv subeq0
      rgen necon3abid biimprd impr absrpcl wfn wf ffn fniniseg simprl mpbir2and
      plyf weq eqeq2d eqeq1 rexbidv elrab sylanbrc elun2 infmrlb expr ralrimiva
      orbi2d imbi2d cq znq qre eqeq1d eqeq2 breq2d orbi12d imbi12d rcla4v com12
      orrd ralrimivv cn0 simprr nnnn0 nnexpcl nnrp rpdivcl simpllr adantl abscl
      rpcnne0 divid nnge1 eqbrtrd ad2antlr lediv23 mpbid orim2d imim2d reximdva
      simpr letrd anassrs mpd ) AGUFZFUFZUGUKZDUHZLMZCUXEMZBUFZCUXEUIUKZUJUHZNU
      LZUMZUNZFUOUPZGUQUPZBOURZUXGUXHUXIUXDEUSUKZUGUKZUXKNULZUMZUNZFUOUPZGUQUPZ
      BOURAUAUFZDUHZLMZCUYEMZUXICUYEUIUKZUJUHZNULZUMZUNZUAPUPZBOURZUXQAQUTZUBUF
      ZCUCUFZUIUKZUJUHZMZUCDVALUTVBZURZUBOVCZVDZPVEVAZVFZORUYGUYHVUGUYJNULZUMZU
      NZUAPUPZUYOAVUEOVUGUYPVUDOQORUYPOVGVHQOVIVJVUCUBOVKVLZAPVUFVMZVUEVNRZVUEV
      OVPZVUEPVGZVUGVUERVUMAPVEVMVUMVQPVEVRVSVTAUYPVNRVUDVNRZVUNQWAAVUCUBWBZVNR
      ZVUDVURVGVUQAVUBVNRZVUSAVUTVUBWDUHDWCUHZNULZADUQWEUHRZDWFVPZVUTVVBSIAVVAW
      FWCUHZVPVVDAELVVAVVEAEUORZELVPJEWGTEVVAHWHWIWJDWFVVAVVEDWFWCWPWKTVUBUQDVU
      BWLWMWNWOUCUBVUBUYTWQTVUDVUROWRVURVUCUBOWSVUROWTXAVURVUDXBXCUYPVUDXDXEVUO
      AQUYPRQVUERVUOQQPXFXHXGQUYPVUDXIVUEQXJXKVTVUPAVUEOPVULXLXMZVTPVUEVUFXNXOX
      PAVUJUAPAUYEPRZSZUYGVUIVVIUYGSZUYHVUHVVIUYGUYHXQZVUHVVIUYGVVKSZSZVUPUDUFZ
      UEUFZNULZUEVUEUPZUDPURZUYJVUERZVUHVUPVVMVVGVTVVRVVMVVPUEOUPZUDPURZVVRLPRL
      VVONULZUEOUPZVWAXTVWBUEOVVOXRUUDVVTVWCUDLPVVNLMVVPVWBUEOVVNLVVONXSYAYBUUA
      VVTVVQUDPVUEOVGVVTVVQUNVULVVPUEVUEOUUBVJYCVJVTVVMUYJVUDRZVVSVVMUYJORZUYJU
      YTMZUCVUBURZVWDVVMUYIYDRUYILVPZVWEVVMUYIVVMCPRZVVHUYIPRAVWIVVHVVLKYEAVVHV
      VLYFZCUYEYGWNYHVVIUYGVVKVWHVVJVWHVVKVVJUYHUYILVVJCYDRUYEYDRZUYILMUYHYIVVJ
      CAVWIVVHUYGKYEYHVVJUYEAVVHUYGYFYHCUYEUUCWNUUEUUFUUGUYIUUHWNVVMUYEVUBRZUYJ
      UYJMZVWGVVMVWLVWKUYGVVMDYDUUIZLYLRVWLVWKUYGSYIAVWNVVHVVLAVVCYDYDDUUJVWNIU
      QDUUOYDYDDUUKYJYELPXTXHYDLUYEDYLUULXCVVMUYEVWJYHVVIUYGVVKUUMUUNUYJWLVWFVW
      MUCUYEVUBUCUAUUPZUYTUYJUYJVWOUYSUYIUJUYRUYECUIYKYMUUQYBXCVUCVWGUBUYJOUYQU
      YJMVUAVWFUCVUBUYQUYJUYTUURUUSUUTUVAUYJVUDUYPUVBTUDUEUYJVUEUVCYNUVDUVRYOUV
      EUYNVUKBVUGOUXIVUGMZUYMVUJUAPVWPUYLVUIUYGVWPUYKVUHUYHUXIVUGUYJNXSUVFUVGYA
      YBWNUYNUXPBOUYNUXNGFUQUOUXCUQRZUXDUORZSZUYNUXNVWSUXEPRZUYNUXNUNVWSUXEUVHR
      VWTUXCUXDUVIUXEUVJTZUYMUXNUAUXEPUYEUXEMZUYGUXGUYLUXMVXBUYFUXFLUYEUXEDWPUV
      KVXBUYHUXHUYKUXLUYEUXECUVLVXBUYJUXKUXINVXBUYIUXJUJUYEUXECUIYKYMUVMUVNUVOU
      VPTUVQUVSYCTAUXPUYDBOAUXIORZSZUXOUYCGUQVXDVWQSUXNUYBFUOVXDVWQVWRUXNUYBUNV
      XDVWSSZUXMUYAUXGVXEUXLUXTUXHVXEUXLUXTVXEUXLSZUXSUXIUXKVXEUXSPRZUXLVXEUXSO
      RZVXGVXEVXCUXRORZVXHAVXCVWSYFZVXEUXRUORZVXIVXEVWREUVTRZVXKVXDVWQVWRUWAAVX
      LVXCVWSAVVFVXLJEUWBTYEUXDEUWCWNZUXRUWDTZUXIUXRUWEWNUXSYPTYQVXFVXCUXIPRZAV
      XCVWSUXLUWFUXIYPZTVXEUXKPRZUXLVXEUXJYDRVXQVXEUXJVXEVWIVWTUXJPRAVWIVXCVWSK
      YEVWSVWTVXDVXAUWGCUXEYGWNYHUXJUWHTYQVXEUXSUXINULZUXLVXEUXIUXIUGUKZUXRNULZ
      VXRVXEVXSQUXRNVXEVXCUXIYDRUXILVPSVXSQMVXJUXIUWIUXIUWJYJVXEVXKQUXRNULVXMUX
      RUWKTUWLVXEVXOVXOLUXIVEULSZUXRPRLUXRVEULSZVXTVXRYIVXCVXOAVWSVXPUWMVXEVXCV
      YAVXJUXIYRYSVXEVXIVYBVXNUXRYRYSUXIUXIUXRUWNYNUWOYQVXEUXLUWSUWTYOUWPUWQUXA
      YTYTUWRUXB $.
      $( [15-Nov-2014] $)

    aalioulem3.e $e |- ( ph -> ( F ` A ) = 0 ) $.
    $( Lemma for ~ aaliou . $)
    aalioulem3 $p |- ( ph -> E. x e. RR+ A. r e. RR ( ( abs ` ( A - r ) ) <_ 1
        -> ( x x. ( abs ` ( F ` r ) ) ) <_ ( abs ` ( A - r ) ) ) ) $=
      ( co cfv c1 cle wbr cr wcel cc cc0 va vc vb cv cmin cabs cmul wi wral crp
      wrex cicc 1re resubcl sylancl peano2re syl ccpn crn cint cres wss cn0 wfn
      caddc fncpn 1nn0 fnfvelrn mp2an intss1 ax-mp cz cply plycpn sseldi df-ima
      cima wf zssre ax-resscn plyss sseli plyreres frn syl5eqss iccssre syl2anc
      3syl cdm syl6ss wceq plyf fdm sseqtr4d c1lip3 wa w3a simp2 recnd 3ad2ant1
      adantr abssub simp3 eqbrtrd wb elicc4abs syl3anc mpbird subid fveq2d abs0
      a1i nn0ge0i eqbrtri syl6eqbr weq fveq2 oveq2d oveq2 breq12d oveq1 rcla42v
      oveq1d simp1l 0cn syl6eqel ffvelrn subid1 breq1d mpd cdiv adantl ad2antrr
      eqtrd wne abscl absge0 syl5ibrcom expimpd remulcl sylibd 3exp com34 com23
      ralrimdv reximdva cif wn recn df-ne biimpri absrpcl syl2an rpreccl ifclda
      1rp wo eqid eqif simprl 0re letri3 simplrr mul02 breqtrd mpbir2and ax-1cn
      mpbi mul01i syl6eq simpllr sylancom rpcnne0 3expb simplr simprr jca leabs
      divrec2 ad2antlr syl31anc letrd clt elrp sylib ledivmul eqbrtrrd sylan2br
      lemul1a jaod expr imim2d ralimdva imbi2d ralbidv rcla4ev ee12an rexlimdva
      mpi ) ACFUDZUELZUFMZNOPZUWTDMZUFMZUAUDZUXBUGLZOPZUHZFQUIZUAQUKZUXCBUDZUXE
      UGLZUXBOPZUHZFQUIZBUJUKZAUBUDZDMZUCUDZDMZUELZUFMZUXFUXRUXTUELZUFMZUGLZOPZ
      UBCNUELZCNVELZULLZUIUCUYJUIZUAQUKUXKAUCUBUYHUYIUADACQRZNQRZUYHQRZJUMCNUNU
      OZAUYLUYIQRZJCUPUQZAURUSZUTZNURMZDQVAZUYTUYRRZUYSUYTVBURVCVDNVCRVUBVFVGVC
      NURVHVIUYTUYRVJVKADVLVMMZRZVUAUYSRHVLDVNUQVOADQVQVUAUSZQDQVPAQQVUAVRZVUEQ
      VBAVUDDQVMMZRVUFHVUCVUGDVLQVBQSVBVUCVUGVBVSVTVLQWAVIWBDWCWHQQVUAWDUQWEAUY
      JSDWIZAUYJQSAUYNUYPUYJQVBUYOUYQUYHUYIWFWGVTWJASSDVRZVUHSWKAVUDVUIHVLDWLUQ
      ZSSDWMUQWNWOAUYKUXJUAQAUXFQRZWPZUYKUXIFQVULUWTQRZUYKUXIVULVUMUXCUYKUXHVUL
      VUMUXCUYKUXHUHVULVUMUXCWQZUYKCDMZUXDUELZUFMZUXGOPZUXHVUNUWTUYJRZCUYJRZUYK
      VURUHVUNVUSUWTCUELUFMZNOPZVUNVVAUXBNOVUNUWTSRZCSRZVVAUXBWKVUNUWTVULVUMUXC
      WRZWSZVUNCVULVUMUYLUXCAUYLVUKJXAWTZWSUWTCXBWGVULVUMUXCXCXDVUNUYLUYMVUMVUS
      VVBXEVVGUYMVUNUMXLVVECNUWTXFXGXHVULVUMVUTUXCAVUTVUKAVUTCCUELZUFMZNOPZAVVI
      TUFMZNOAVVHTUFAVVDVVHTWKACJWSCXIUQXJVVKTNOXKNVGXMXNXOAUYLUYMUYLVUTVVJXEJU
      YMAUMXLJCNCXFXGXHXAWTUYGVURUXSUXDUELZUFMZUXFUXRUWTUELZUFMZUGLZOPUCUBUWTCU
      YJUYJUCFXPZUYCVVMUYFVVPOVVQUYBVVLUFVVQUYAUXDUXSUEUXTUWTDXQXRXJVVQUYEVVOUX
      FUGVVQUYDVVNUFUXTUWTUXRUEXSXJXRXTUXRCWKZVVMVUQVVPUXGOVVRVVLVUPUFVVRUXSVUO
      UXDUEUXRCDXQYCXJVVRVVOUXBUXFUGVVRVVNUXAUFUXRCUWTUEYAXJXRXTYBWGVUNVUQUXEUX
      GOVUNVUQUXDVUOUELZUFMZUXEVUNVUOSRUXDSRZVUQVVTWKVUNVUOTSVUNAVUOTWKAVUKVUMU
      XCYDKUQZYEYFVUNVUIVVCVWAVULVUMVUIUXCAVUIVUKVUJXAWTVVFSSUWTDYGZWGZVUOUXDXB
      WGVUNVVSUXDUFVUNVVSUXDTUELZUXDVUNVUOTUXDUEVWBXRVUNVWAVWEUXDWKVWDUXDYHUQYN
      XJYNYIUUAUUBUUCUUDUUEUUFYJAUXJUXQUAQVULUXFTWKZNNUXFUFMZYKLZUUGZUJRUXJUXCV
      WIUXEUGLZUXBOPZUHZFQUIZUXQVULVWFNVWHUJNUJRVULVWFWPUUPXLVULVWFUUHZWPVWGUJR
      ZVWHUJRVULUXFSRZUXFTYOZVWOVWNVUKVWPAUXFUUIYLVWQVWNUXFTUUJZUUKUXFUULZUUMVW
      GUUNUQUUOVULUXIVWLFQVULVUMWPUXHVWKUXCVULVUMUXHVWKVULVUMUXHWPZWPZVWFVWINWK
      ZWPZVWNVWIVWHWKZWPZUUQZVWKVWIVWIWKVXFVWIUURVWFVWINVWHUUSUVHVXAVXCVWKVXEVX
      AVWFVXBVWKVXAVWFWPZVWKVXBNUXEUGLZUXBOPVXGVXHTUXBOVXGVXHNTUGLTVXGUXETNUGVX
      GUXETWKZUXETOPZTUXEOPZVXGUXEQRZTQRVXIVXJVXKWPXEVXAVXLVWFVXAVWAVXLVXAVUIVV
      CVWAAVUIVUKVWTVUJYMVXAUWTVULVUMUXHUUTZWSVWCWGZUXDYPUQZXAUVAUXETUVBUOVXGUX
      EUXGTOVULVUMUXHVWFUVCVXGUXGTUXBUGLZTVWFUXGVXPWKVXAUXFTUXBUGYAYLVXGUXBSRZV
      XPTWKVXAVXQVWFVXAUXBVXAUXASRZUXBQRZVXAUXAVXAUYLVUMUXAQRAUYLVUKVWTJYMVXMCU
      WTUNWGWSZUXAYPUQZWSXAUXBUVDUQYNUVEVXGVWAVXKVXAVWAVWFVXNXAUXDYQUQUVFXRNUVG
      UVIUVJVXGVXRTUXBOPZVXAVXRVWFVXTXAUXAYQZUQXDVXBVWJVXHUXBOVWINUXEUGYAYIYRYS
      VXAVWNVXDVWKVXAVWNWPVWKVXDVWHUXEUGLZUXBOPZVWNVXAVWQVYEVWRVXAVWQWPZUXEVWGY
      KLZVYDUXBOVYFUXESRZVWGSRZVWGTYOZWPZVYGVYDWKZVYFUXEVXAVXLVWQVXOXAZWSVYFVWO
      VYKVXAVWQVWPVWOVYFUXFAVUKVWTVWQUVKWSVWSUVLZVWGUVMUQVYHVYIVYJVYLUXEVWGUVSU
      VNWGVYFVYGUXBOPZUXEVWGUXBUGLZOPZVXAVYQVWQVXAUXEUXGVYPVXOVXAVUKVXSUXGQRAVU
      KVWTUVOZVYAUXFUXBYTWGVXAVWGQRZVXSVYPQRVXAVWPVYSVXAUXFVYRWSUXFYPUQZVYAVWGU
      XBYTWGVULVUMUXHUVPVXAVUKVYSVXSVYBWPUXFVWGOPZUXGVYPOPVYRVYTVXAVXSVYBVYAVXA
      VXRVYBVXTVYCUQUVQVUKWUAAVWTUXFUVRUVTUXFVWGUXBUWIUWAUWBXAVYFVXLVXSVYSTVWGU
      WCPWPZVYOVYQXEVYMVXAVXSVWQVYAXAVYFVWOWUBVYNVWGUWDUWEUXEUXBVWGUWFXGXHUWGUW
      HVXDVWJVYDUXBOVWIVWHUXEUGYAYIYRYSUWJUWSUWKUWLUWMUXPVWMBVWIUJUXLVWIWKZUXOV
      WLFQWUCUXNVWKUXCWUCUXMVWJUXBOUXLVWIUXEUGYAYIUWNUWOUWPUWQUWRYJ $.
      $( [15-Nov-2014] $)

    $( Lemma for ~ aaliou . $)
    aalioulem4 $p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN (
        ( ( F ` ( p / q ) ) =/= 0 /\ ( abs ` ( A - ( p / q ) ) ) <_ 1 ) ->
          ( A = ( p / q ) \/ ( x / ( q ^ N ) ) <_
            ( abs ` ( A - ( p / q ) ) ) ) ) ) $=
      ( co cfv cle wbr cmul cr wcel syl va cv cmin cabs c1 wi wral crp wrex cc0
      cdiv wne wa wceq wo cn cz aalioulem3 w3a cq simp2l simp2r znq syl2anc qre
      cexp simp3r oveq2 fveq2d breq1d fveq2 oveq2d breq12d imbi12d rcla4v com23
      sylc simp1r nnrp simp1l nnz 3syl rpexpcl rpdivcl rpre adantr cc cply plyf
      recnd ffvelrn abscl remulcl resubcl rpcn rpne0 divrec syl3anc rpge0 absid
      wf absmul oveq1d eqtrd cdgr mulcom oveq2i syl6eq aalioulem1 simp3l mulne0
      eqeltrd syl22anc nnabscl eqeltrrd nnge1 clt 1re a1i sylib ledivmul mpbird
      wb elrp rpreccl lemul2 mpbid eqbrtrd simpr letrd olcd syld 3exp ralrimdvv
      ex com34 reximdva mpd ) ACUAUBZUCMZUDNZUEOPZBUBZYSDNZUDNZQMZUUAOPZUFZUARU
      GZBUHUIGUBZFUBZUKMZDNZUJULZCUULUCMZUDNZUEOPZUMZCUULUNZUUCUUKEVFMZUKMZUUPO
      PZUOZUFZFUPUGGUQUGZBUHUIABCDEUAHIJKLURAUUIUVEBUHAUUCUHSZUMZUUIUVDGFUQUPUV
      GUUJUQSZUUKUPSZUMZUUIUVDUVGUVJUURUUIUVCUVGUVJUURUUIUVCUFUVGUVJUURUSZUUIUU
      CUUMUDNZQMZUUPOPZUVCUVKUULRSZUUQUUIUVNUFUVKUULUTSZUVOUVKUVHUVIUVPUVGUVHUV
      IUURVAZUVGUVHUVIUURVBZUUJUUKVCVDUULVETZUVGUVJUUNUUQVGUVOUUIUUQUVNUUHUUQUV
      NUFUAUULRYSUULUNZUUBUUQUUGUVNUVTUUAUUPUEOUVTYTUUOUDYSUULCUCVHVIZVJUVTUUFU
      VMUUAUUPOUVTUUEUVLUUCQUVTUUDUUMUDYSUULDVKVIVLUWAVMVNVOVPVQUVKUVNUVCUVKUVN
      UMZUVBUUSUWBUVAUVMUUPUVKUVARSZUVNUVKUVAUHSZUWCUVKUVFUUTUHSZUWDAUVFUVJUURV
      RZUVKUUKUHSZEUQSZUWEUVKUVIUWGUVRUUKVSTUVKAEUPSUWHAUVFUVJUURVTZJEWAWBUUKEW
      CVDZUUCUUTWDVDUVAWETWFUVKUVMRSZUVNUVKUUCRSZUVLRSZUWKUVKUVFUWLUWFUUCWETUVK
      UUMWGSZUWMUVKWGWGDXAZUULWGSUWNUVKADUQWHNSZUWOUWIIUQDWIWBUVKUULUVSWJWGWGUU
      LDWKVDZUUMWLTZUUCUVLWMVDWFUVKUUPRSZUVNUVKUUOWGSUWSUVKUUOUVKCRSZUVOUUORSUV
      KAUWTUWIKTUVSCUULWNVDWJUUOWLTWFUVKUVAUVMOPUVNUVKUVAUUCUEUUTUKMZQMZUVMOUVK
      UUCWGSZUUTWGSZUUTUJULZUVAUXBUNUVKUVFUXCUWFUUCWOTUVKUWEUXDUWJUUTWOTZUVKUWE
      UXEUWJUUTWPTZUUCUUTWQWRUVKUXAUVLOPZUXBUVMOPZUVKUXHUEUUTUVLQMZOPZUVKUXJUPS
      UXKUVKUUTUUMQMZUDNZUXJUPUVKUXMUUTUDNZUVLQMZUXJUVKUXDUWNUXMUXOUNUXFUWQUUTU
      UMXBVDUVKUXNUUTUVLQUVKUUTRSZUJUUTOPZUXNUUTUNUVKUWEUXPUWJUUTWETUVKUWEUXQUW
      JUUTWSTUUTWTVDXCXDUVKUXLUQSUXLUJULZUXMUPSUVKUXLUUMUUKDXENZVFMZQMZUQUVKUXL
      UUMUUTQMZUYAUVKUXDUWNUXLUYBUNUXFUWQUUTUUMXFVDUUTUXTUUMQEUXSUUKVFHXGXGXHUV
      KDUUJUUKUVKAUWPUWIITUVQUVRXIXLUVKUXDUXEUWNUUNUXRUXFUXGUWQUVGUVJUUNUUQXJUU
      TUUMXKXMUXLXNVDXOUXJXPTUVKUERSZUWMUXPUJUUTXQPUMZUXHUXKYCUYCUVKXRXSUWRUVKU
      WEUYDUWJUUTYDXTUEUVLUUTYAWRYBUVKUXARSZUWMUWLUJUUCXQPUMZUXHUXIYCUVKUWEUXAU
      HSUYEUWJUUTYEUXAWEWBUWRUVKUVFUYFUWFUUCYDXTUXAUVLUUCYFWRYGYHWFUVKUVNYIYJYK
      YOYLYMYPVPYNYQYR $.
      $( [16-Nov-2014] $)

    $d N a b x $.

    $( Lemma for ~ aaliou . $)
    aalioulem5 $p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN (
        ( F ` ( p / q ) ) =/= 0 -> ( A = ( p / q ) \/ ( x / ( q ^ N ) ) <_
            ( abs ` ( A - ( p / q ) ) ) ) ) ) $=
      ( c1 cle wbr wa crp wcel cr syl va cv cdiv co cfv cc0 cmin cabs wceq cexp
      wne wo wi cn wral cz wrex aalioulem4 cif simpr 1rp a1i syl2anc clt adantr
      ifcl w3a simprr nnrp ad2antrr nnz rpexpcl rpdivcl rpre 1re znq qre adantl
      cc cq resubcl recnd abscl 3jca rpreccl simplr min2 wb elrp lediv1 syl3anc
      sylib mpbid cmul cn0 nnnn0 nnexpcl 1nn nnmulcl nnge1 ledivmul mpbird ltle
      letrd sylancr imp jca letr sylc olcd pm3.21 min1 anim1i ex orim2d imim12d
      pm2.61ltlei anassrs ralimdva breq1d orbi2d imbi2d 2ralbidv rcla4ev ee12an
      a1d oveq1 rexlimdva mpd ) AGUBZFUBZUCUDZDUEUFUKZCYLUGUDZUHUEZMNOZPZCYLUIZ
      UAUBZYKEUJUDZUCUDZYONOZULZUMZFUNUOZGUPUOZUAQUQYMYRBUBZYTUCUDZYONOZULZUMZF
      UNUOGUPUOZBQUQZAUACDEFGHIJKLURAUUFUUMUAQAYSQRZPZYSMNOZYSMUSZQRZUUFYMYRUUQ
      YTUCUDZYONOZULZUMZFUNUOZGUPUOZUUMUUOUUNMQRZUURAUUNUTUVEUUOVAVBUUPYSMQVFVC
      ZUUOUUEUVCGUPUUOYJUPRZPUUDUVBFUNUUOUVGYKUNRZUUDUVBUMZUUOUVGUVHPZPZUVIMYOU
      VKMYOVDOZPZUVBUUDUVMUVAYMUVMUUTYRUVMUUSSRZMSRZYOSRZVGZUUSMNOZMYONOZPUUTUV
      KUVQUVLUVKUVNUVOUVPUVKUUSQRZUVNUVKUURYTQRZUVTUUOUURUVJUVFVEZUVKYKQRZEUPRZ
      UWAUVKUVHUWCUUOUVGUVHVHZYKVITUVKEUNRZUWDAUWFUUNUVJJVJZEVKTYKEVLVCZUUQYTVM
      VCUUSVNTZUVOUVKVOVBZUVKYNVSRUVPUVKYNUVKCSRZYLSRZYNSRAUWKUUNUVJKVJUVJUWLUU
      OUVJYLVTRUWLYJYKVPYLVQTVRCYLWAVCWBYNWCTZWDVEUVMUVRUVSUVKUVRUVLUVKUUSMYTUC
      UDZMUWIUVKUWNQRZUWNSRUVKUWAUWOUWHYTWETUWNVNTUWJUVKUUQMNOZUUSUWNNOZUVKYSSR
      ZUVOUWPUVKUUNUWRAUUNUVJWFZYSVNTZUWJYSMWGVCUVKUUQSRZUVOYTSRUFYTVDOPZUWPUWQ
      WHUVKUURUXAUWBUUQVNTZUWJUVKUWAUXBUWHYTWIWLZUUQMYTWJWKWMUVKUWNMNOZMYTMWNUD
      ZNOZUVKUXFUNRZUXGUVKYTUNRZMUNRZUXHUVKUVHEWORZUXIUWEUVKUWFUXKUWGEWPTYKEWQV
      CUXJUVKWRVBYTMWSVCUXFWTTUVKUVOUVOUXBUXEUXGWHUWJUWJUXDMMYTXAWKXBXDVEUVKUVL
      UVSUVKUVOUVPUVLUVSUMVOUWMMYOXCXEXFXGUUSMYOXHXIXJYFYFUVKYPPZYMYQUUCUVAYPYM
      YQUMUVKYPYMXKVRUXLUUBUUTYRUVKUUBUUTUMYPUVKUUBUUTUVKUUBPUVNUUASRZUVPVGZUUS
      UUANOZUUBPUUTUVKUXNUUBUVKUVNUXMUVPUWIUVKUUAQRZUXMUVKUUNUWAUXPUWSUWHYSYTVM
      VCUUAVNTUWMWDVEUVKUXOUUBUVKUUQYSNOZUXOUVKUWRUVOUXQUWTUWJYSMXLVCUVKUXAUWRU
      XBUXQUXOWHUXCUWTUXDUUQYSYTWJWKWMXMUUSUUAYOXHXIXNVEXOXPUWJUWMXQXRXSXSUULUV
      DBUUQQUUGUUQUIZUUKUVBGFUPUNUXRUUJUVAYMUXRUUIUUTYRUXRUUHUUSYONUUGUUQYTUCYG
      XTYAYBYCYDYEYHYI $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aaliou . $)
    aalioulem6 $p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN
        ( A = ( p / q ) \/ ( x / ( q ^ N ) ) <_
          ( abs ` ( A - ( p / q ) ) ) ) ) $=
      ( cle wbr cn wral wa crp wcel cr va vb cv cdiv co cfv wceq cexp cmin cabs
      cc0 wo wi cz wne wrex aalioulem2 aalioulem5 reeanv sylanbrc r19.26-2 ifcl
      cif simpr w3a ad2antlr nnrp ad2antll ad2antrr nnz rpexpcl syl2anc rpdivcl
      adantl syl rpre simplrl cc cq znq qre resubcl recn abscl 3syl 3jca adantr
      simplrr min1 clt wb elrp sylib lediv1 syl3anc mpbid anim1i letr ex orim2d
      sylc embantd adantrd min2 adantld pm2.61dane ralimdva oveq1 breq1d orbi2d
      anassrs 2ralbidv rcla4ev ee12an syl5bir rexlimdvva mpd ) AGUCZFUCZUDUEZDU
      FZUKUGZCXTUGZUAUCZXSEUHUEZUDUEZCXTUIUEZUJUFZMNZULZUMZFOPGUNPZYAUKUOZYCUBU
      CZYEUDUEZYHMNZULZUMZFOPGUNPZQZUBRUPUARUPZYCBUCZYEUDUEZYHMNZULZFOPGUNPZBRU
      PZAYLUARUPYSUBRUPUUAAUACDEFGHIJKUQAUBCDEFGHIJKLURYLYSUAUBRRUSUTAYTUUGUAUB
      RRYTYKYRQZFOPZGUNPZAYDRSZYNRSZQZQZUUGYKYRGFUNOVAUUNYDYNMNZYDYNVCZRSZUUJYC
      UUPYEUDUEZYHMNZULZFOPZGUNPZUUGUUMUUQAUUOYDYNRVBZVNUUNUUIUVAGUNUUNXRUNSZQU
      UHUUTFOUUNUVDXSOSZUUHUUTUMZUUNUVDUVEQZQZUVFYAUKUVHYBQZYKUUTYRUVIYBYJUUTUV
      HYBVDUVIYIUUSYCUVHYIUUSUMYBUVHYIUUSUVHYIQUURTSZYFTSZYHTSZVEZUURYFMNZYIQUU
      SUVHUVMYIUVHUVJUVKUVLUVHUURRSZUVJUVHUUQYERSZUVOUUMUUQAUVGUVCVFZUVHXSRSZEU
      NSZUVPUVEUVRUUNUVDXSVGVHUVHEOSZUVSAUVTUUMUVGJVIEVJVOXSEVKVLZUUPYEVMVLUURV
      PVOZUVHYFRSZUVKUVHUUKUVPUWCAUUKUULUVGVQZUWAYDYEVMVLYFVPVOUVHYGTSZYGVRSUVL
      UVHCTSZXTTSZUWEAUWFUUMUVGKVIUVGUWGUUNUVGXTVSSUWGXRXSVTXTWAVOVNCXTWBVLYGWC
      YGWDWEZWFWGUVHUVNYIUVHUUPYDMNZUVNUVHYDTSZYNTSZUWIUVHUUKUWJUWDYDVPVOZUVHUU
      LUWKAUUKUULUVGWHZYNVPVOZYDYNWIVLUVHUUPTSZUWJYETSUKYEWJNQZUWIUVNWKUVHUUQUW
      OUVQUUPVPVOZUWLUVHUVPUWPUWAYEWLWMZUUPYDYEWNWOWPWQUURYFYHWRXAWSWGWTXBXCUVH
      YMQZYRUUTYKUWSYMYQUUTUVHYMVDUWSYPUUSYCUVHYPUUSUMYMUVHYPUUSUVHYPQUVJYOTSZU
      VLVEZUURYOMNZYPQUUSUVHUXAYPUVHUVJUWTUVLUWBUVHYORSZUWTUVHUULUVPUXCUWMUWAYN
      YEVMVLYOVPVOUWHWFWGUVHUXBYPUVHUUPYNMNZUXBUVHUWJUWKUXDUWLUWNYDYNXDVLUVHUWO
      UWKUWPUXDUXBWKUWQUWNUWRUUPYNYEWNWOWPWQUURYOYHWRXAWSWGWTXBXEXFXKXGXGUUFUVB
      BUUPRUUBUUPUGZUUEUUTGFUNOUXEUUDUUSYCUXEUUCUURYHMUUBUUPYEUDXHXIXJXLXMXNXOX
      PXQ $.
      $( [16-Nov-2014] $)

    $( Liouville's theorem on diophantine approximation:  Any algebraic number,
       being a root of a polynomial ` F ` in integer coefficients, is not
       approximable beyond order ` N = deg ( F ) ` by rational numbers.  In
       this form, it also applies to rational numbers themselves, which are not
       well approximable by other rational numbers.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    aaliou $p |- ( ph -> E. x e. RR+ A. p e. ZZ A. q e. NN ( A = ( p / q ) \/
      ( x / ( q ^ N ) ) < ( abs ` ( A - ( p / q ) ) ) ) ) $=
      ( cdiv co wbr cn cz crp wcel cr va cv wceq cexp cmin cabs cfv cle wo wral
      wrex clt aalioulem6 wa rphalfcl adantl w3a ad2antlr nnrp ad2antll nnz syl
      c2 wi ad2antrr rpexpcl syl2anc rpdivcl rpre simplr adantr znq qre resubcl
      cc cq syl2an recn abscl 3syl 3jca rphalflt cc0 wb elrp sylib ltdiv1 mpbid
      syl3anc anim1i ltletr sylsyld orim2d anassrs ralimdva oveq1 breq1d orbi2d
      ex 2ralbidv rcla4ev ee12an rexlimdva mpd ) ACGUBZFUBZMNZUCZUAUBZXFEUDNZMN
      ZCXGUENZUFUGZUHOZUIZFPUJZGQUJZUARUKXHBUBZXJMNZXMULOZUIZFPUJGQUJZBRUKZAUAC
      DEFGHIJKLUMAXQYCUARAXIRSZUNZXIVCMNZRSZXQXHYFXJMNZXMULOZUIZFPUJZGQUJZYCYDY
      GAXIUOZUPYEXPYKGQYEXEQSZUNXOYJFPYEYNXFPSZXOYJVDYEYNYOUNZUNZXNYIXHYQYHTSZX
      KTSZXMTSZUQXNYHXKULOZXNUNZYIYQYRYSYTYQYHRSZYRYQYGXJRSZUUCYDYGAYPYMURZYQXF
      RSZEQSZUUDYOUUFYEYNXFUSUTAUUGYDYPAEPSUUGJEVAVBVEXFEVFVGZYFXJVHVGYHVIVBYQX
      KRSZYSYQYDUUDUUIAYDYPVJUUHXIXJVHVGXKVIVBYQXLTSZXLVOSYTYECTSZXGTSZUUJYPAUU
      KYDKVKYPXGVPSUULXEXFVLXGVMVBCXGVNVQXLVRXLVSVTWAYQXNUUBYQUUAXNYQYFXIULOZUU
      AYDUUMAYPXIWBURYQYFTSZXITSZXJTSWCXJULOUNZUUMUUAWDYQYGUUNUUEYFVIVBYDUUOAYP
      XIVIURYQUUDUUPUUHXJWEWFYFXIXJWGWIWHWJWSYHXKXMWKWLWMWNWOWOYBYLBYFRXRYFUCZY
      AYJGFQPUUQXTYIXHUUQXSYHXMULXRYFXJMWPWQWRWTXAXBXCXD $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph k a b c d $.  $d A k a b c d $.  $d B k a b c d $.  $d C k a b c d $.
    $d F a b c d $.
    geolim3.a $e |- ( ph -> A e. ZZ ) $.
    geolim3.b1 $e |- ( ph -> B e. CC ) $.
    geolim3.b2 $e |- ( ph -> ( abs ` B ) < 1 ) $.
    geolim3.c $e |- ( ph -> C e. CC ) $.
    geolim3.f $e |- F = ( k e. ( ZZ>= ` A ) |-> ( C x. ( B ^ ( k - A ) ) ) ) $.
    $( Geometric series convergence with arbitrary shift, radix, and
       multiplicative constant.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    geolim3 $p |- ( ph -> seq A ( + , F ) ~~> ( C / ( 1 - B ) ) ) $=
      ( caddc co cexp cmul c1 wceq cc0 wcel cc va cseq cuz cfv cv cmin cmpt cli
      cdiv seqeq3 ax-mp wbr cneg cshi cn0 cvv nn0uz cz 0z ovex oveq2 eqid fvmpt
      a1i adantl geolim expcl sylan eqeltrd zcn syl nn0cn mptex shftval4 syl2an
      wa fvex uzaddcl oveq1 oveq2d pncan2 eqtr4d 3eqtrd isermulc2 negid seqeq1d
      uzid wne ax-1cn subcl sylancr cabs cle 1re 1nn0 nn0ge0i absid mp2an abscl
      cr clt ltne syl3anc eqnetrd fveq2 necon3i subeq0 necon3bid mpbird 3brtr4d
      wb divrec znegcl isershft syl5eqbr ) ALFBUBZLEBUCUDZDCEUEZBUFMZNMZOMZUGZB
      UBZDPCUFMZUIMZUHFYBQXPYCQKLFYBBUJUKAYCYEUHULZLYBBUMZUNMZBYGLMZUBZYEUHULZA
      LYHRUBDPYDUIMZOMZYJYEUHAYLDUAEUOCXRNMZUGZYHRUPUOUQRURSAUSVDYLUPSAPYDUIUTV
      DJACUAYOHIUAUEZUOSZYPYOUDZCYPNMZQAEYPYNYSUOYOXRYPCNVAYOVBCYPNUTVCVEZVFAYQ
      VPZYRYSTYTACTSZYQYSTSHCYPVGVHVIUUAYPYHUDZBYPLMZYBUDZDCUUDBUFMZNMZOMZDYROM
      ABTSZYPTSZUUCUUEQYQABURSZUUIGBVJVKZYPVLZBYPYBEXQYABUCVQVMZVNVOUUAUUDXQSZU
      UEUUHQABXQSZYQUUOAUUKUUPGBWGVKYPBBVRVHEUUDYAUUHXQYBXRUUDQZXTUUGDOUUQXSUUF
      CNXRUUDBUFVSVTVTYBVBDUUGOUTVCVKUUAUUGYRDOUUAUUGYSYRUUAUUFYPCNAUUIUUJUUFYP
      QYQUULUUMBYPWAVOVTYTWBVTWCWDAYIRLYHAUUIYIRQUULBWEVKWFADTSYDTSZYDRWHZYEYMQ
      JAPTSZUUBUURWIHPCWJWKAUUSPCWHZAPWLUDZCWLUDZWHUVAAUVBPUVCUVBPQZAPWTSZRPWMU
      LUVDWNPWOWPPWQWRVDAUVCWTSZUVEUVCPXAULPUVCWHAUUBUVFHCWSVKUVEAWNVDIUVCPXBXC
      XDPCUVBUVCPCWLXEXFVKAYDRPCAUUTUUBYDRQPCQXKWIHPCXGWKXHXIDYDXLXCXJAUUKYGURS
      ZYEUPSZYFYKXKGAUUKUVGGBXMVKUVHADYDUIUTVDYELYBBYGUPUUNXNXCXIXO $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d A a k x p q $.

    $( Liouville's approximation theorem for algebraic numbers per se.
       (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    aaliou2 $p |- ( A e. ( AA i^i RR ) -> E. k e. NN E. x e. RR+ A. p e. ZZ
        A. q e. NN ( A = ( p / q ) \/ ( x / ( q ^ k ) ) <
            ( abs ` ( A - ( p / q ) ) ) ) ) $=
      ( va caa wcel cv cdiv co wceq cfv cn wral cz wrex cc cc0 c0p cr cexp cmin
      cin wa cabs clt wbr wo crp elin cply csn cdif wi elaa w3a cdgr cxp eldifn
      wn 3ad2ant1 simpr fveq1 adantl simpl2 simpl3 recnd fvex fvconst2 3eqtr3rd
      sneqd xpeq2d eqtrd df-0p syl6eqr elsn sylibr mtand wb eldifi 0dgrb mtbird
      syl cn0 dgrcl elnn0 sylib orel2 sylc eqid simp3 simp2 aaliou oveq2 oveq2d
      breq1d orbi2d 2ralbidv rexbidv rcla4ev syl2anc 3exp rexlimiv sylbi imp )
      BGUAUDHBGHZBUAHZUEBEIDIZJKZLZAIZXICIZUBKZJKZBXJUCKUFMZUGUHZUIZDNOEPOZAUJQ
      ZCNQZBGUAUKXGXHYAXGBRHZBFIZMZSLZFPULMZTUMZUNZQZUEXHYAUOZBFUPYIYJYBYEYJFYH
      YCYHHZYEXHYAYKYEXHUQZYCURMZNHZXKXLXIYMUBKZJKZXPUGUHZUIZDNOEPOZAUJQZYAYLYM
      SLZVAYNUUAUIZYNYLUUAYCRSYCMZUMZUSZLZYLUUFYCYGHZYKYEUUGVAXHYCYFYGUTVBYLUUF
      UEZYCTLUUGUUHYCRSUMZUSZTUUHYCUUEUUJYLUUFVCUUHUUDUUIRUUHUUCSUUHYDBUUEMZSUU
      CUUFYDUUKLYLBYCUUEVDVEYKYEXHUUFVFUUHYBUUKUUCLUUHBYKYEXHUUFVGVHRUUCBSYCVIV
      JWDVKVLVMVNVOVPFTVQVRVSYLYCYFHZUUAUUFVTYKYEUULXHYCYFYGWAVBZPYCWBWDWCYLYMW
      EHZUUBYLUULUUNUUMPYCWFWDYMWGWHUUAYNWIWJZYLABYCYMDEYMWKUUMUUOYKYEXHWLYKYEX
      HWMWNXTYTCYMNXMYMLZXSYSAUJUUPXRYREDPNUUPXQYQXKUUPXOYPXPUGUUPXNYOXLJXMYMXI
      UBWOWPWQWRWSWTXAXBXCXDVEXEXFXE $.
      $( [16-Nov-2014] $)

    $( Liouville's approximation theorem extended to complex ` A ` .
       (Contributed by Stefan O'Rear, 20-Nov-2014.) $)
    aaliou2b $p |- ( A e. AA -> E. k e. NN E. x e. RR+ A. p e. ZZ
        A. q e. NN ( A = ( p / q ) \/ ( x / ( q ^ k ) ) <
            ( abs ` ( A - ( p / q ) ) ) ) ) $=
      ( wcel cr cdiv co wceq cfv clt wbr cn cz crp wa c1 cc0 syl cexp cmin cabs
      caa cv wo wral wrex cin elin aaliou2 sylbir wn cim c2 1nn a1i cc wne aacn
      adantr imcl wb reim0b necon3bbid biimpa absrpcl syl2anc rphalfcl cn0 1nn0
      recnd nnexpcl mpan2 ad2antll nnrp rpdivcl rpre znq adantl qre subcl abscl
      cq cle nnge1 1rp rpregt0 ax-mp lediv2 syl3anc mpbid rpcn breqtrd rphalflt
      div1 imsub reim0 oveq2d subid1 3eqtrd fveq2d absimle ltletrd lelttrd olcd
      eqbrtrrd ralrimivva oveq2 breq1d orbi2d 2ralbidv oveq1 rcla42ev pm2.61dan
      ) BUDFZBGFZBEUEZDUEZHIZJZAUEZXSCUEZUAIZHIZBXTUBIZUCKZLMZUFZDNUGEOUGZAPUHC
      NUHZXPXQQBUDGUIFYKBUDGUJABCDEUKULXPXQUMZQZRNFZBUNKZUCKZUOHIZPFZYAYQXSRUAI
      ZHIZYGLMZUFZDNUGEOUGZYKYNYMUPUQYMYPPFZYRYMYOURFZYOSUSZUUDYMYOYMBURFZYOGFX
      PUUGYLBUTZVAZBVBTVLZXPYLUUFXPXQYOSXPUUGXQYOSJVCUUHBVDTVEVFYOVGVHZYPVITZYM
      UUBEDONYMXROFZXSNFZQZQZUUAYAUUPYTYQYGUUPYTPFZYTGFUUPYRYSPFZUUQYMYRUUOUULV
      AZUUPYSNFZUURUUNUUTYMUUMUUNRVJFUUTVKXSRVMVNVOZYSVPTZYQYSVQVHYTVRTUUPYRYQG
      FZUUSYQVRTZUUPYFURFZYGGFUUPUUGXTURFZUVEYMUUGUUOUUIVAZUUPXTUUPXTWDFZXTGFZU
      UOUVHYMXRXSVSVTXTWATZVLZBXTWBVHZYFWCTZUUPYTYQRHIZYQWEUUPRYSWEMZYTUVNWEMZU
      UPUUTUVOUVAYSWFTUUPRGFSRLMQZYSGFSYSLMQZUVCSYQLMQZUVOUVPVCUVQUUPRPFUVQWGRW
      HWIUQUUPUURUVRUVBYSWHTUUPYRUVSUUSYQWHTRYSYQWJWKWLUUPYQURFZUVNYQJUUPYRUVTU
      USYQWMTYQWPTWNUUPYQYPYGUVDUUPUUDYPGFYMUUDUUOUUKVAZYPVRTUVMUUPUUDYQYPLMUWA
      YPWOTUUPYFUNKZUCKZYPYGWEUUPUWBYOUCUUPUWBYOXTUNKZUBIZYOSUBIZYOUUPUUGUVFUWB
      UWEJUVGUVKBXTWQVHUUPUWDSYOUBUUPUVIUWDSJUVJXTWRTWSUUPUUEUWFYOJYMUUEUUOUUJV
      AYOWTTXAXBUUPUVEUWCYGWEMUVLYFXCTXGXDXEXFXHYJUUCYAYBYSHIZYGLMZUFZDNUGEOUGC
      ARYQNPYCRJZYIUWIEDONUWJYHUWHYAUWJYEUWGYGLUWJYDYSYBHYCRXSUAXIWSXJXKXLYBYQJ
      ZUWIUUBEDONUWKUWHUUAYAUWKUWGYTYGLYBYQYSHXMXJXKXLXNWKXO $.
      $( [20-Nov-2014] $)
  $}

  ${
    $d F b c d $.  $d A a b c d $.  $d B a b c d $.  $d G a b d $.
    aaliou3lem.a $e |- G = ( c e. ( ZZ>= ` A ) |->
        ( ( 2 ^ -u ( ! ` A ) ) x. ( ( 1 / 2 ) ^ ( c - A ) ) ) ) $.
    $( Lemma for ~ aaliou3 . $)
    aaliou3lem1 $p |- ( ( A e. NN /\ B e. ( ZZ>= ` A ) ) ->
        ( G ` B ) e. RR ) $=
      ( cn wcel cfv c2 cexp co cmin cmul cr wceq oveq2d crp cz 2re elrpii wa c1
      cuz cfa cneg cdiv oveq1 ovex fvmpt adantl 2pos cn0 simpl nnnn0 faccl 3syl
      cv nnz znegcl rpexpcl sylancr 2ne0 halfgt0 eluzelz zsubcl syl2anr rpmulcl
      rereccli syl2anc rpre syl eqeltrd ) AFGZBAUCHZGZUAZBCHZIAUDHZUEZJKZUBIUFK
      ZBALKZJKZMKZNVOVQWDOVMDBVTWADUQZALKZJKZMKWDVNCWEBOZWGWCVTMWHWFWBWAJWEBALU
      GPPEVTWCMUHUIUJVPWDQGZWDNGVPVTQGZWCQGZWIVPIQGVSRGZWJISUKTVPVRFGZVRRGWLVPV
      MAULGWMVMVOUMAUNAUOUPVRURVRUSUPIVSUTVAVPWAQGWBRGZWKWAISVBVHVCTVOBRGARGWNV
      MABVDAURBAVEVFWAWBUTVAVTWCVGVIWDVJVKVL $.
      $( [16-Nov-2014] $)

    aaliou3lem.b $e |- F = ( a e. NN |-> ( 2 ^ -u ( ! ` a ) ) ) $.
    $( Lemma for ~ aaliou3 . $)
    aaliou3lem2 $p |- ( ( A e. NN /\ B e. ( ZZ>= ` A ) ) ->
        ( F ` B ) e. ( 0 (,] ( G ` B ) ) ) $=
      ( wcel cfv co wbr cle c2 cexp wceq oveq2d syl c1 cmul vb vd cn cuz wa cc0
      cioc cr clt cxr w3a wb 0xr aaliou3lem1 elioc2 sylancr crp cfa cneg sselda
      uznnssnn cv fveq2 negeqd ovex fvmpt cz 2re 2pos elrpii cn0 nnnn0 3syl nnz
      faccl znegcl rpexpcl eqeltrd rpre rpgt0 wi caddc breq12d imbi2d cdiv cmin
      weq leid cc nncn subid 2ne0 rereccli recni exp0 ax-mp syl6eq mulid1 eqtrd
      rpcn breqtrrd uzid oveq1 3brtr4d a1i rpge0 halfgt0 eluzelz zsubcl syl2anr
      simpl rpmulcl syl2anc jca31 adantr adantl simpr zcn mulneg1 nnmulcl nnge1
      zmulcl 1re nnre leneg mpbid eqbrtrd 1nn0 nn0negzi eluz mpbird 1lt2 ltleii
      sylancl expwordi mp3an12 2cn expn1 ax-1cn syl3anc syl6breq lemul12a facp1
      3impia syl112anc addcom peano2cn adddi oveq1d 3eqtr3d wne rpcnne0 expaddz
      mpan addsub eluzle znn0sub2 expp1 mulass eqtr4d sylibrd peano2nn peano2uz
      ex 3imtr4d expcom a2d uzind4 impcom mpbir3and ) AUCIZBAUDJZIZUEZBCJZUFBDJ
      ZUGKIZUVOUHIZUFUVOUILZUVOUVPMLZUVNUFUJIUVPUHIUVQUVRUVSUVTUKULUMABDFGUNUFU
      VPUVOUOUPUVNUVOUQIZUVRUVNUVONBURJZUSZOKZUQUVNBUCIZUVOUWDPUVKUVLUCBAVAZUTZ
      EBNEVBZURJZUSZOKZUWDUCCUWHBPZUWJUWCNOUWLUWIUWBUWHBURVCVDQHNUWCOVEVFRUVNNU
      QIZUWCVGIZUWDUQINVHVIVJZUVNUWBUCIZUWBVGIUWNUVNUWEBVKIUWPUWGBVLBVOVMUWBVNU
      WBVPVMNUWCVQUPVRZUVOVSRUVNUWAUVSUWQUVOVTRUVMUVKUVTUVKUAVBZCJZUWRDJZMLZWAU
      VKACJZADJZMLZWAZUVKUBVBZCJZUXFDJZMLZWAUVKUXFSWBKZCJZUXJDJZMLZWAUVKUVTWAUA
      UBABUWRAPZUXAUXDUVKUXNUWSUXBUWTUXCMUWRACVCUWRADVCWCWDUAUBWGZUXAUXIUVKUXOU
      WSUXGUWTUXHMUWRUXFCVCUWRUXFDVCWCWDUWRUXJPZUXAUXMUVKUXPUWSUXKUWTUXLMUWRUXJ
      CVCUWRUXJDVCWCWDUWRBPZUXAUVTUVKUXQUWSUVOUWTUVPMUWRBCVCUWRBDVCWCWDUXEAVGIZ
      UVKNAURJZUSZOKZUYASNWEKZAAWFKZOKZTKZUXBUXCMUVKUYAUYAUYEMUVKUYAUQIZUYAUHIU
      YAUYAMLUVKUWMUXTVGIZUYFUWOUVKUXSUCIZUXSVGIZUYGUVKAVKIZUYHAVLZAVOZRUXSVNZU
      XSVPZVMNUXTVQZUPZUYAVSUYAWHVMUVKUYEUYASTKZUYAUVKUYDSUYATUVKUYDUYBUFOKZSUV
      KUYCUFUYBOUVKAWIIZUYCUFPAWJZAWKRQUYBWIIZUYRSPUYBNVHWLWMZWNZUYBWOWPWQQUVKU
      YFUYAWIIZUYQUYAPUYPUYAWTZUYAWRVMWSXAEAUWKUYAUCCUWHAPZUWJUXTNOVUFUWIUXSUWH
      AURVCVDQHNUXTOVEVFUVKUXRAUVLIUXCUYEPAVNZAXBFAUYAUYBFVBZAWFKZOKZTKZUYEUVLD
      VUHAPZVUJUYDUYATVULVUIUYCUYBOVUHAAWFXCQQGUYAUYDTVEVFVMXDXEUXFUVLIZUVKUXIU
      XMUVKVUMUXIUXMWAUVKVUMUEZNUXFURJZUSZOKZUYAUYBUXFAWFKZOKZTKZMLZNUXJURJZUSZ
      OKZUYAUYBUXJAWFKZOKZTKZMLZUXIUXMVUNVVAVUQNVUPUXFTKZOKZTKZVUTUYBTKZMLZVVHV
      UNVVAVVMVUNVVAUEVUQUHIZUFVUQMLZUEVUTUHIZUEZVVJUHIZUFVVJMLZUEUYBUHIZUEZVVA
      VVJUYBMLZVVMVUNVVQVVAVUNVVNVVOVVPVUNVUQUQIZVVNVUNUWMVUPVGIZVWCUWOVUNVUOUC
      IZVUOVGIVWDVUNUXFUCIZUXFVKIZVWEUVKUVLUCUXFUWFUTZUXFVLZUXFVOVMZVUOVNVUOVPV
      MZNVUPVQUPZVUQVSRVUNVWCVVOVWLVUQXFRVUNVUTUQIZVVPVUNUYFVUSUQIZVWMVUNUWMUYG
      UYFUWOVUNUYHUYIUYGVUNUVKUYJUYHUVKVUMXKUYKUYLVMUYMUYNVMUYOUPZVUNUYBUQIVURV
      GIZVWNUYBVUBXGVJVUMUXFVGIZUXRVWPUVKAUXFXHZVUGUXFAXIXJUYBVURVQUPZUYAVUSXLX
      MVUTVSRXNXOVUNVWAVVAVUNVVRVVSVVTVUNVVJUQIZVVRVUNUWMVVIVGIZVWTUWOVUNVWDVWQ
      VXAVWKVUMVWQUVKVWRXPZVUPUXFYBXMZNVVIVQUPZVVJVSRVUNVWTVVSVXDVVJXFRVVTVUNVU
      BXEXNXOVUNVVAXQVUNVWBVVAVUNVVJNSUSZOKZUYBMVUNVXEVVIUDJIZVVJVXFMLZVUNVXGVV
      IVXEMLZVUNVVIVUOUXFTKZUSZVXEMVUNVUOWIIZUXFWIIZVVIVXKPVUNVWEVXLVWJVUOWJRZV
      UNVWQVXMVXBUXFXRZRZVUOUXFXSXMVUNSVXJMLZVXKVXEMLZVUNVXJUCIZVXQVUNVWEVWFVXS
      VWJVWHVUOUXFXTXMZVXJYARVUNSUHIVXJUHIZVXQVXRULYCVUNVXSVYAVXTVXJYDRSVXJYEUP
      YFYGVUNVXAVXEVGIVXGVXIULVXCSYHYIVVIVXEYJYNYKNUHISNMLVXGVXHVHSNYCVHYLYMNVV
      IVXEYOYPRNWIIZVXFUYBPYQNYRWPUUAXOVVQVWAVVAVWBUEVVMVUQVUTVVJUYBUUBUUDUUEUV
      DVUNVVDVVKVVGVVLMVUNVVDNVUPVVIWBKZOKZVVKVUNVVCVYCNOVUNVVCVUOUXJTKZUSZVYCV
      UNVVBVYEVUNVWFVWGVVBVYEPVWHVWIUXFUUCVMVDVUNVUPUXJTKZVUPSUXFWBKZTKZVYFVYCV
      UNUXJVYHVUPTVUNVXMSWIIZUXJVYHPVXPYSUXFSUUFYNQVUNVXLUXJWIIZVYGVYFPVXNVUNVW
      QVXMVYKVXBVXOUXFUUGVMVUOUXJXSXMVUNVYIVUPSTKZVVIWBKZVYCVUNVUPWIIZVYJVXMVYI
      VYMPVUNVWDVYNVWKVUPXRZRVYJVUNYSXEZVXPVUPSUXFUUHYTVUNVYLVUPVVIWBVUNVWDVYNV
      YLVUPPVWKVYOVUPWRVMUUIWSUUJWSQVUNVWDVXAVYDVVKPZVWKVXCVYBNUFUUKUEZVWDVXAUE
      VYQUWMVYRUWONUULWPNVUPVVIUUMUUNXMWSVUNVVGUYAVUSUYBTKZTKZVVLVUNVVFVYSUYATV
      UNVVFUYBVURSWBKZOKZVYSVUNVVEWUAUYBOVUNVXMVYJUYSVVEWUAPVXPVYPUVKUYSVUMUYTX
      OUXFSAUUOYTQVUNVUAVURVKIZWUBVYSPVUCVUNUXRVWQAUXFMLZWUCUVKUXRVUMVUGXOVXBVU
      MWUDUVKAUXFUUPXPAUXFUUQYTUYBVURUURUPWSQVUNVUDVUSWIIZVUAVVLVYTPVUNUYFVUDVW
      OVUERVUNVWNWUEVWSVUSWTRVUAVUNVUCXEUYAVUSUYBUUSYTUUTWCUVAVUNUXGVUQUXHVUTMV
      UNVWFUXGVUQPVWHEUXFUWKVUQUCCEUBWGZUWJVUPNOWUFUWIVUOUWHUXFURVCVDQHNVUPOVEV
      FRVUMUXHVUTPUVKFUXFVUKVUTUVLDFUBWGZVUJVUSUYATWUGVUIVURUYBOVUHUXFAWFXCQQGU
      YAVUSTVEVFXPWCVUNUXKVVDUXLVVGMVUNVWFUXJUCIUXKVVDPVWHUXFUVBEUXJUWKVVDUCCUW
      HUXJPZUWJVVCNOWUHUWIVVBUWHUXJURVCVDQHNVVCOVEVFVMVUMUXLVVGPZUVKVUMUXJUVLIW
      UIAUXFUVCFUXJVUKVVGUVLDVUHUXJPZVUJVVFUYATWUJVUIVVEUYBOVUHUXJAWFXCQQGUYAVV
      FTVEVFRXPWCUVEUVFUVGUVHUVIUVJ $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aaliou3 . $)
    aaliou3lem3 $p |- ( A e. NN -> ( seq A ( + , F ) e. dom ~~> /\
        sum_ b e. ( ZZ>= ` A ) ( F ` b ) e. RR+ /\
        sum_ b e. ( ZZ>= ` A ) ( F ` b ) <_
          ( 2 x. ( 2 ^ -u ( ! ` A ) ) ) ) ) $=
      ( wcel cfv crp c2 co cle wbr syl cc0 c1 cdiv cc cn caddc cseq cli cdm cuz
      cv csu cfa cneg cexp cmul eqid cz nnz uzid aaliou3lem1 wa cr clt cioc w3a
      aaliou3lem2 cxr 0xr elioc2 sylancr mpbid simp1d cmin 2cn 2ne0 reccli cabs
      wb a1i wceq 2re rereccli halfgt0 elrpii rprege0 mp2b halflt1 eqbrtri 2pos
      absid cn0 nnnn0 faccl znegcl 3syl rpexpcl rpcn geolim3 breldm simp2d elrp
      seqex sylanbrc rpge0 simp3d cvgcmp eqidd isumrpcl eqid1 isumle recnd ovex
      cvv isumclim ax-1cn 2halves ax-mp subaddrii oveq2i mulcl sylancl div1 wne
      1rp rpcnne0 divdiv2 mp3an23 mulcom 3eqtr4d syl5eq eqtrd breqtrd 3jca ) AU
      AIZUBBAUCUDUEZIAUFJZEUGZBJZEUHZKIYPLLAUIJZUJZUKMZULMZNOYKECBAAYMYMUMZYKAU
      NIAYMIAUOZAUPPZAYNCFGUQZYKYNYMIURZYOUSIZQYOUTOZYOYNCJZNOZUUEYOQUUHVAMIZUU
      FUUGUUIVBZAYNBCDFGHVCUUEQVDIUUHUSIUUJUUKVOVEUUDQUUHYOVFVGVHZVIZYKUBCAUCZY
      SRRLSMZVJMZSMZUDOUUNYLIYKAUUOYSFCUUBUUOTIYKLVKVLVMZVPUUOVNJZRUTOYKUUSUUOR
      UTUUOKIUUOUSIQUUONOURUUSUUOVQUUOLVRVLVSVTWAUUOWBUUOWGWCWDWEVPYKYSKIZYSTIZ
      YKLKIZYRUNIZUUTLVRWFWAZYKYQUAIZYQUNIUVCYKAWHIUVEAWIAWJPYQUOYQWKWLLYRWMVGY
      SWNPZGWOZUUNUUQUDUBCAWSWPPZUUEYOKIZQYONOUUEUUFUUGUVIUUMUUEUUFUUGUUIUULWQY
      OWRWTZYOXAPUUEUUFUUGUUIUULXBZXCZYKYOEBAAYMYMUUAUUAUUCUUEYOXDZUVJUVLXEYKYP
      YMUUHEUHZYTNYKYOUUHEBCAYMYMXFUUBUVMUUMUUEUUHXDZUUDUVKUVLUVHXGYKUVNUUQYTYK
      UUHUUQECAXJYMUUAUUBUVOUUEUUHUUDXHUUQXJIYKYSUUPSXIVPUVGXKYKUUQYSUUOSMZYTUU
      PUUOYSSRUUOUUOXLUURUURRTIZUUOUUOUBMRVQXLRXMXNXOXPYKYSLULMZRSMZUVRUVPYTYKU
      VRTIZUVSUVRVQYKUVALTIZUVTUVFVKYSLXQXRUVRXSPYKUVAUVPUVSVQZUVFUVAUVQRQXTURZ
      UWALQXTURZUWBRKIUWCYARYBXNUVBUWDUVDLYBXNYSRLYCYDPYKUWAUVAYTUVRVQVKUVFLYSY
      EVGYFYGYHYIYJ $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d A a b x $.  $d B a b x $.

    $( Lemma for ~ aaliou3 .  Exponential sequences are cofinal in the
       reals. $)
    aaliou3lem8 $p |- ( ( ( A e. RR /\ 1 < A ) /\ B e. RR ) -> E. x e. NN B <
        ( A ^ x ) ) $=
      ( cr wcel c1 clt wbr wa co cexp cz cle cfv cc0 crp a1i 1re syl syl2anc va
      cv caddc wrex cn cif clog cdiv cfl wne 1rp wn simplr lt01 wo simpr letric
      sylancl orcanai ltletrd elrp sylanbrc ifclda relogcl simpll lttrd syl3anc
      0re ltne logne0 redivcl flcl ifcl sylancr peano2zdi rpre max1 cmul flltp1
      rpexpcl zre rplogcl adantr sylib ltdivmul2 mpbid relogexp breqtrrd logltb
      wb mpbird lelttrd oveq1 oveq2d breq2d rcla4ev simprl 1z elnnz1 simprr cuz
      wceq simplll wi ltle mpd max2 eluz expwordi oveq2 exp32 rexlimdv ) BDEZFB
      GHZIZCDEZIZCBUAUBZFUCJZKJZGHZUALUDZCBAUBZKJZGHZAUEUDZXQCFMHZFCUFZUGNZBUGN
      ZUHJZUINZLEZCBYLFUCJZKJZGHZYBXQYKDEZYMXQYIDEZYJDEZYJOUJZYQXQYHPEZYRXQYGFC
      PFPEXQYGIUKQXQYGULZIZXPOCGHCPEXOXPUUBUMZUUCOFCODEZUUCVHQFDEZUUCRQUUDOFGHZ
      UUCUNQXQYGFCMHZXQXPUUFYGUUHUOXOXPUPZRCFUQURUSUTCVAVBVCZYHVDSZXQBPEZYSXQXM
      OBGHUULXMXNXPVEZXQOFBUUEXQVHQUUFXQRQZUUMUUGXQUNQXMXNXPUMZVFBVAVBZBVDSXQUU
      LBFUJZYTUUPXQUUFXMXNUUQUUNUUMUUOFBVIVGBVJTYIYJVKVGZYKVLSZXQCYHYOUUIXQUUFX
      PYHDERUUIYGFCDVMVNXQYOPEZYODEXQUULYNLEZUUTUUPXQYLUUSVOZBYNVTTZYOVPSXQXPUU
      FCYHMHUUIRCFVQURXQYHYOGHZYIYOUGNZGHZXQYIYNYJVRJZUVEGXQYKYNGHZYIUVGGHZXQYQ
      UVHUURYKVSSXQYRYNDEZYSOYJGHIZUVHUVIWJUUKXQUVAUVJUVBYNWASXQYJPEZUVKXOUVLXP
      BWBWCYJVAWDYIYNYJWEVGWFXQUULUVAUVEUVGXBUUPUVBBYNWGTWHXQUUAUUTUVDUVFWJUUJU
      VCYHYOWITWKWLYAYPUAYLLXRYLXBZXTYOCGUVMXSYNBKXRYLFUCWMWNWOWPTXQYAYFUALXQXR
      LEZYAYFXQUVNYAIZIZFXSMHZXSFUFZUEEZCBUVRKJZGHZYFUVPUVRLEZFUVRMHZUVSUVPXSLE
      ZFLEUWBUVPXRXQUVNYAWQVOZWRUVQXSFLVMURZUVPUUFXSDEZUWCRUVPUWDUWGUWEXSWASZFX
      SVQVNUVRWSVBUVPCXTUVTXOXPUVOUMUVPXTPEZXTDEUVPUULUWDUWIXQUULUVOUUPWCZUWEBX
      SVTTXTVPSUVPUVTPEZUVTDEUVPUULUWBUWKUWJUWFBUVRVTTUVTVPSXQUVNYAWTUVPXMFBMHZ
      UVRXSXANEZXTUVTMHXMXNXPUVOXCXQUWLUVOXQXNUWLUUOXQUUFXMXNUWLXDRUUMFBXEVNXFW
      CUVPUWMXSUVRMHZUVPUUFUWGUWNRUWHFXSXGVNUVPUWDUWBUWMUWNWJUWEUWFXSUVRXHTWKBX
      SUVRXIVGUTYEUWAAUVRUEYCUVRXBYDUVTCGYCUVRBKXJWOWPTXKXLXF $.
      $( [20-Nov-2014] $)

    $( Lemma for ~ aaliou3 . $)
    aaliou3lem9 $p |- ( ( A e. NN /\ B e. RR+ ) -> E. x e. NN ( 2 x. ( 2 ^ -u (
        ! ` ( x + 1 ) ) ) ) <_ ( B / ( ( 2 ^ ( ! ` x ) ) ^ A ) ) ) $=
      ( cn wcel crp c2 cdiv co cexp wbr c1 cmul cle cr syl2anc syl wceq syl3anc
      cc va wa cv clt wrex caddc cfa cfv cneg 2re 1lt2 pm3.2i rerpdivcl sylancr
      simpr aaliou3lem8 cmin simprl simpll nnaddm1cl simplr cn0 reexpcl cz 2pos
      nnnn0 elrpii nnaddcl nnm1nn0 peano2nn0 faccl zmulcl zsubcl rpexpcl simprr
      nnz rpre wi ltle mpd cuz a1i 1re ltleii cc0 nnre nnge1 lemulge12 syl22anc
      nn0ge0 facp1 oveq1d nn0cn subdi ax-1cn npcan pncan eqtrd 3eqtr2d breqtrrd
      nncn oveq2d wb eluz mpbird expwordi letrd wne rpcnne0 expsub syl12anc 2cn
      ax-mp expmul rpcn rpne0 3eqtrrd rpreccl rpmulcl elrp sylib ledivmul mpbid
      divrec mul12 breqtrd expneg eqtr4d 3brtr4d fveq2d negeqd breq12d rexlimdv
      oveq1 fveq2 rcla4ev exp32 ) BDEZCFEZUBZGCHIZGUAUCZJIZUDKZUADUEZGGAUCZLUFI
      ZUGUHZUIZJIZMIZCGUUFUGUHZJIZBJIZHIZNKZADUEZYTGOEZLGUDKZUBUUAOEZUUEUURUUSU
      JUKULYTUURYSUUTUJYRYSUOGCUMZUNUAGUUAUPUNYTUUDUUQUADYTUUBDEZUUDUUQYTUVBUUD
      UBZUBZUUBBUFIZLUQIZDEZGGUVFLUFIZUGUHZUIZJIZMIZCGUVFUGUHZJIZBJIZHIZNKZUUQU
      VDUVBYRUVGYTUVBUUDURZYRYSUVCUSZUUBBUTPUVDGGUVIJIZHIZCLUVOHIZMIZUVLUVPNUVD
      UWAUWCNKZGUVTUWCMIZNKZUVDGCUVTUWBMIZMIZUWENUVDUUAUWGNKZGUWHNKZUVDUUAGUVIU
      VMBMIZUQIZJIZUWGNUVDUUAUUCUWMUVDUURYSUUTUJYRYSUVCVAZUVAUNZUVDUURUUBVBEZUU
      COEZUJUVDUVBUWPUVRUUBVFQZGUUBVCUNZUVDUWMFEZUWMOEUVDGFEZUWLVDEZUWTGUJVEVGZ
      UVDUVIVDEZUWKVDEZUXBUVDUVIDEZUXDUVDUVHVBEZUXFUVDUVFVBEZUXGUVDUVEDEZUXHUVD
      UVBYRUXIUVRUVSUUBBVHPZUVEVIQZUVFVJQZUVHVKQZUVIVPQZUVDUVMVDEZBVDEZUXEUVDUV
      MDEZUXOUVDUXHUXQUXKUVFVKQZUVMVPQZUVDYRUXPUVSBVPQZUVMBVLPZUVIUWKVMPZGUWLVN
      UNUWMVQQUVDUUDUUAUUCNKZYTUVBUUDVOUVDUUTUWQUUDUYCVRUWOUWSUUAUUCVSPVTUVDUUR
      LGNKZUWLUUBWAUHEZUUCUWMNKUURUVDUJWBZUYDUVDLGWCUJUKWDWBUVDUYEUUBUWLNKZUVDU
      UBUVMUUBMIZUWLNUVDUUBOEZUVMOEZWEUUBNKZLUVMNKZUUBUYHNKUVDUVBUYIUVRUUBWFQUV
      DUXQUYJUXRUVMWFQUVDUWPUYKUWRUUBWJQUVDUXQUYLUXRUVMWGQUUBUVMWHWIUVDUWLUVMUV
      HMIZUWKUQIZUVMUVHBUQIZMIZUYHUVDUVIUYMUWKUQUVDUXHUVIUYMRUXKUVFWKQWLUVDUVMT
      EZUVHTEZBTEZUYPUYNRUVDUXQUYQUXRUVMXAQUVDUXGUYRUXLUVHWMQUVDYRUYSUVSBXAQZUV
      MUVHBWNSUVDUYOUUBUVMMUVDUYOUVEBUQIZUUBUVDUVHUVEBUQUVDUVETEZLTEZUVHUVERUVD
      UXIVUBUXJUVEXAQVUCUVDWOWBUVELWPPWLUVDUUBTEZUYSVUAUUBRUVDUVBVUDUVRUUBXAQUY
      TUUBBWQPWRXBWSWTUVDUUBVDEZUXBUYEUYGXCUVDUVBVUEUVRUUBVPQUYBUUBUWLXDPXEGUUB
      UWLXFSXGUVDUWMUVTGUWKJIZHIZUVTUVOHIZUWGUVDGTEZGWEXHUBZUXDUXEUWMVUGRVUJUVD
      UXAVUJUXCGXIXMWBUXNUYAGUVIUWKXJXKUVDVUFUVOUVTHUVDVUIUVMVBEZBVBEZVUFUVORVU
      IUVDXLWBZUVDUXQVUKUXRUVMVFQUVDYRVULUVSBVFQGUVMBXNSXBUVDUVTTEZUVOTEZUVOWEX
      HZVUHUWGRUVDUVTFEZVUNUVDUXAUXDVUQUXCUXNGUVIVNUNZUVTXOQZUVDUVOFEZVUOUVDUVN
      FEZUXPVUTUVDUXAUXOVVAUXCUXSGUVMVNUNUXTUVNBVNPZUVOXOQZUVDVUTVUPVVBUVOXPQZU
      VTUVOYDSXQWTUVDUURUWGOEZCOEWECUDKUBZUWIUWJXCUYFUVDUWGFEZVVEUVDVUQUWBFEZVV
      GVURUVDVUTVVHVVBUVOXRQZUVTUWBXSPUWGVQQUVDYSVVFUWNCXTYAGUWGCYBSYCUVDCTEZVU
      NUWBTEZUWHUWERUVDYSVVJUWNCXOQZVUSUVDVVHVVKVVIUWBXOQCUVTUWBYESYFUVDUURUWCO
      EZUVTOEWEUVTUDKUBZUWDUWFXCUYFUVDUWCFEZVVMUVDYSVVHVVOUWNVVICUWBXSPUWCVQQUV
      DVUQVVNVURUVTXTYAGUWCUVTYBSXEUVDUVLGLUVTHIZMIZUWAUVDUVKVVPGMUVDVUIUVIVBEZ
      UVKVVPRXLUVDUXFVVRUXMUVIVFQGUVIYGUNXBUVDVUIVUNUVTWEXHZUWAVVQRVUMVUSUVDVUQ
      VVSVURUVTXPQGUVTYDSYHUVDVVJVUOVUPUVPUWCRVVLVVCVVDCUVOYDSYIUUPUVQAUVFDUUFU
      VFRZUUKUVLUUOUVPNVVTUUJUVKGMVVTUUIUVJGJVVTUUHUVIVVTUUGUVHUGUUFUVFLUFYNYJY
      KXBXBVVTUUNUVOCHVVTUUMUVNBJVVTUULUVMGJUUFUVFUGYOXBWLXBYLYPPYQYMVT $.
      $( [20-Nov-2014] $)
  $}

  ${
    $d a b c d e $.  $d F b c d e $.  $d L c d e f $.  $d H b d e f $.
    $d A a b c d e $.  $d B a b c d e $.
    aaliou3lem.c $e |- F = ( a e. NN |-> ( 2 ^ -u ( ! ` a ) ) ) $.
    aaliou3lem.d $e |- L = sum_ b e. NN ( F ` b ) $.
    aaliou3lem.e $e |- H = ( c e. NN |-> sum_ b e. ( 1 ... c ) ( F ` b ) ) $.
    $( Lemma for ~ aaliou3 . $)
    aaliou3lem4 $p |- L e. RR $=
      ( c1 cfv cv csu cr cn wcel c2 cexp co cmul cuz nnuz sumeq1i eqtri crp 1nn
      cseq cli cdm cfa cneg cle wbr cdiv cmin cmpt eqid aaliou3lem3 simp2d rpre
      caddc mp2b eqeltri ) CJUAKZELAKZEMZNCOVEEMVFHOVDVEEUBUCUDJOPZVFUEPZVFNPUF
      VGVAAJUGUHUIPVHVFQQJUJKUKRSZTSULUMJAFVDVIJQUNSFLJUOSRSTSUPZDEFVJUQGURUSVF
      UTVBVC $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aaliou3 . $)
    aaliou3lem5 $p |- ( A e. NN -> ( H ` A ) e. RR ) $=
      ( cn wcel cfv c1 cfz co cv cr c2 cexp csu oveq2 sumeq1d sumex fvmpt fzfid
      wceq wa elfznn adantl cfa cneg weq fveq2 negeqd oveq2d ovex crp cz elrpii
      2re 2pos cn0 nnnn0 faccl syl nnz znegcl 3syl rpexpcl sylancr rpre eqeltrd
      fsumrecl ) AKLZACMNAOPZFQZBMZFUAZRGANGQZOPZVRFUAVSKCVTAUGWAVPVRFVTANOUBUC
      JVPVRFUDUEVOVPVRFVONAUFVOVQVPLZUHVQKLZVRRLWBWCVOVQAUIUJWCVRSVQUKMZULZTPZR
      EVQSEQZUKMZULZTPWFKBEFUMZWIWESTWJWHWDWGVQUKUNUOUPHSWETUQUEWCWFURLZWFRLWCS
      URLWEUSLZWKSVAVBUTWCWDKLZWDUSLWLWCVQVCLWMVQVDVQVEVFWDVGWDVHVISWEVJVKWFVLV
      FVMVFVNVM $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aaliou3 . $)
    aaliou3lem6 $p |- ( A e. NN -> ( ( H ` A ) x.
          ( 2 ^ ( ! ` A ) ) ) e. ZZ ) $=
      ( cn wcel c2 cexp co cmul cz cc cn0 syl cfv cfa c1 cfz wceq oveq2 sumeq1d
      csu sumex fvmpt oveq1d fzfid crp 2re 2pos elrpii nnnn0 faccl 3syl rpexpcl
      cv nnz sylancr rpcn wa cneg elfznn fveq2 negeqd oveq2d ovex adantl znegcl
      weq eqeltrd fsummulc1 caddc adantr cc0 rpcnne0 ax-mp expaddz mpan syl2anc
      wne 2z cmin zcn addcom nncn negsub eqtrd cle wbr elfzle2 facwordi syl3anc
      wb nn0sub mpbid zexpcl eqeltrrd fsumzcl ) AKLZACUAZMAUBUAZNOZPOUCAUDOZFVA
      ZBUAZFUHZXGPOZQXDXEXKXGPGAUCGVAZUDOZXJFUHXKKCXMAUEXNXHXJFXMAUCUDUFUGJXHXJ
      FUIUJUKXDXLXHXJXGPOZFUHQXDXHXJXGFXDUCAULZXDXGUMLZXGRLXDMUMLZXFQLZXQMUNUOU
      PZXDASLZXFKLZXSAUQZAURZXFVBUSZMXFUTVCXGVDTXDXIXHLZVEZXJMXIUBUAZVFZNOZRYFX
      JYJUEZXDYFXIKLZYKXIAVGZEXIMEVAZUBUAZVFZNOYJKBEFVNZYPYIMNYQYOYHYNXIUBVHVIV
      JHMYINVKUJTVLZYGYJUMLZYJRLYGXRYIQLZYSXTYGYHKLZYHQLYTYGYLXISLZUUAYFYLXDYMV
      LZXIUQZXIURZUSZYHVBYHVMUSZMYIUTVCYJVDTVOVPXDXHXOFXPYGXOYJXGPOZQYGXJYJXGPY
      RUKYGMYIXFVQOZNOZUUHQYGYTXSUUJUUHUEZUUGXDXSYFYEVRZMRLMVSWEVEZYTXSVEUUKXRU
      UMXTMVTWAMYIXFWBWCWDYGMQLUUISLUUJQLWFYGUUIXFYHWGOZSYGUUIXFYIVQOZUUNYGYIRL
      ZXFRLZUUIUUOUEYGYTUUPUUGYIWHTYGXSUUQUULXFWHTZYIXFWIWDYGUUQYHRLZUUOUUNUEUU
      RYGUUAUUSUUFYHWJTXFYHWKWDWLYGYHXFWMWNZUUNSLZYGUUBYAXIAWMWNZUUTYGYLUUBUUCU
      UDTZXDYAYFYCVRZYFUVBXDXIUCAWOVLXIAWPWQYGYHSLZXFSLZUUTUVAWRYGUUBUUAUVEUVCU
      UEYHUQUSYGYAYBUVFUVDYDXFUQUSYHXFWSWDWTVOMUUIXAVCXBVOXCVOVO $.
      $( [16-Nov-2014] $)

    $( Lemma for ~ aaliou3 . $)
    aaliou3lem7 $p |- ( A e. NN -> ( ( H ` A ) =/= L /\
        ( abs ` ( L - ( H ` A ) ) ) <_
          ( 2 x. ( 2 ^ -u ( ! ` ( A + 1 ) ) ) ) ) ) $=
      ( cn wcel c1 caddc co cfv crp c2 cexp wbr cuz cv csu cfa cneg cmul cle wa
      wne cmin cabs cseq cli cdm w3a peano2nn cdiv cmpt eqid aaliou3lem3 3simpc
      3syl wceq wb cfz cc nncn ax-1cn pncan sylancl oveq2d sumeq1d oveq1d eqid1
      nnuz eqidd weq fveq2 negeqd ovex fvmpt cz 2re 2pos elrpii cn0 nnnn0 faccl
      syl nnz znegcl rpexpcl sylancr rpcn eqeltrd adantl simp1d ax-mp isumsplit
      1nn a1i oveq2 sumex 3eqtr4rd syl6eqr aaliou3lem4 recni aaliou3lem5 simp2d
      recnd subadd syl3anc mpbird eqcomd eleq1 breq1 cr clt adantr simprl difrp
      anbi12d ltne necomd rpmulcl rpre absdifle resubcl syl2anc ltsubrp wi ltle
      lttrd mpd simprr lesubadd2 mpbid mpbir2and jca ex sylbid ) AKLZAMNOZUAPZF
      UBZBPZFUCZQLZUUGRRUUCUDPZUEZSOZUFOZUGTZUHZACPZDUIZDUUOUJOZUKPUULUGTZUHZUU
      BUUCKLZNBUUCULUMUNZLZUUHUUMUOUUNAUPZUUCBGUUDUUKMRUQOZGUBZUUCUJOSOUFOURZEF
      GUVFUSHUTZUVBUUHUUMVAVBUUBUUNUUQQLZUUQUULUGTZUHZUUSUUBUUGUUQVCZUUNUVJVDUU
      BUUQUUGUUBUUQUUGVCZUUOUUGNOZDVCZUUBUVMKUUFFUCZDUUBMUUCMUJOZVEOZUUFFUCZUUG
      NOMAVEOZUUFFUCZUUGNOUVOUVMUUBUVRUVTUUGNUUBUVQUVSUUFFUUBUVPAMVEUUBAVFLMVFL
      UVPAVCAVGVHAMVIVJVKVLVMUUBUUFFBMUUCUUDKVOUUDVNUVCUUBUUEKLZUHUUFVPUWAUUFVF
      LUUBUWAUUFRUUEUDPZUEZSOZVFEUUEREUBZUDPZUEZSOUWDKBEFVQZUWGUWCRSUWHUWFUWBUW
      EUUEUDVRVSVKHRUWCSVTWAUWAUWDQLZUWDVFLUWARQLZUWCWBLZUWIRWCWDWEZUWAUWBKLZUW
      BWBLUWKUWAUUEWFLUWMUUEWGUUEWHWIUWBWJUWBWKVBRUWCWLWMUWDWNWIWOWPNBMULUVALZU
      UBMKLZUWNWTUWOUWNMUAPZUUFFUCZQLUWQRRMUDPUESOZUFOUGTMBGUWPUWRUVDUVEMUJOSOU
      FOURZEFGUWSUSHUTWQWRXAWSUUBUUOUVTUUGNGAMUVEVEOZUUFFUCUVTKCUVEAVCUWTUVSUUF
      FUVEAMVEXBVLJUVSUUFFXCWAVMXDIXEUUBDVFLZUUOVFLUUGVFLZUVLUVNVDUXAUUBDBCDEFG
      HIJXFZXGXAUUBUUOABCDEFGHIJXHZXJUUBUUTUUHUXBUVCUUTUVBUUHUUMUVGXIUUGWNVBDUU
      OUUGXKXLXMXNUVKUUHUVHUUMUVIUUGUUQQXOUUGUUQUULUGXPYBWIUUBUVJUUSUUBUVJUHZUU
      PUURUXEDUUOUXEUUOXQLZDXQLZUUODXRTZDUUOUIUUBUXFUVJUXDXSZUXGUXEUXCXAZUXEUXH
      UVHUUBUVHUVIXTUXEUXFUXGUXHUVHVDUXIUXCUUODYAVJXMZUUODYCXLYDUXEUURUUOUULUJO
      ZDUGTZDUUOUULNOUGTZUXEUXGUXFUULXQLZUURUXMUXNUHVDUXJUXIUXEUULQLZUXOUUBUXPU
      VJUUBUWJUUKQLZUXPUWLUUBUWJUUJWBLZUXQUWLUUBUUIKLZUUIWBLUXRUUBUUTUUCWFLUXSU
      VCUUCWGUUCWHVBUUIWJUUIWKVBRUUJWLWMRUUKYEWMXSZUULYFWIZDUUOUULYGXLUXEUXLDXR
      TZUXMUXEUXLUUODUXEUXFUXOUXLXQLZUXIUYAUUOUULYHYIZUXIUXJUXEUXFUXPUXLUUOXRTU
      XIUXTUUOUULYJYIUXKYMUXEUYCUXGUYBUXMYKUYDUXCUXLDYLVJYNUXEUVIUXNUUBUVHUVIYO
      UXEUXGUXFUXOUVIUXNVDUXJUXIUYADUUOUULYPXLYQYRYSYTUUAYN $.
      $( [16-Nov-2014] $)

    $d L a b c d e f $.

    $( Example of a "Liouville number", a very simple definable transcedental
       real.  (Contributed by Stefan O'Rear, 20-Nov-2014.) $)
    aaliou3 $p |- -. L e. AA $=
      ( vf vd wcel cdiv co cn cz crp wn c2 cr ve caa cv wceq cexp cmin cabs cfv
      clt wbr wo wral wrex wa c1 cfa cneg cmul aaliou3lem9 aaliou3lem6 ad2antrl
      caddc cle cn0 2nn nnnn0 faccl 3syl nnexpcl sylancr wne cc cc0 aaliou3lem5
      recnd nnne0 divcan4 syl3anc aaliou3lem7 simpld eqnetrd necomd df-ne sylib
      nncn syl aaliou3lem4 nnre remulcl syl2anc nnrp rerpdivcl resubcl 2re 2pos
      abscl elrpii peano2nn0 nnz znegcl rpexpcl rpmulcl simplr ad2antrr rpdivcl
      oveq2d fveq2d simprd eqbrtrd simprr letrd lenlt mpbid oveq1 eqeq2d notbid
      rpre wb breq2d anbi12d oveq2 rcla42ev syl112anc exp32 rexlimdv mpd pm4.56
      breq12d rexbii rexnal bitri nrexdv nrex aaliou2b mto ) CUBLCJUCZKUCZMNZUD
      ZEUCZYQDUCZUENZMNZCYRUFNZUGUHZUIUJZUKZKOULZJPULZEQUMZDOUMUUJDOUUAOLZUUIEQ
      UUKYTQLZUNZYSRZUUFRZUNZKOUMZJPUMZUUIRZUUMSSUAUCZUOVBNZUPUHZUQZUENZURNZYTS
      UUTUPUHZUENZUUAUENZMNZVCUJZUAOUMUURUAUUAYTUSUUMUVJUURUAOUUMUUTOLZUVJUURUU
      MUVKUVJUNZUNZUUTBUHZUVGURNZPLZUVGOLZCUVOUVGMNZUDZRZUVICUVRUFNZUGUHZUIUJZR
      ZUURUVKUVPUUMUVJUUTABCDEFGHIUTVAUVMSOLUVFVDLZUVQVEUVMUUTVDLZUVFOLUWEUVKUW
      FUUMUVJUUTVFVAZUUTVGUVFVFVHSUVFVIVJZUVMCUVRVKUVTUVMUVRCUVMUVRUVNCUVMUVNVL
      LUVGVLLZUVGVMVKZUVRUVNUDUVMUVNUVKUVNTLZUUMUVJUUTABCDEFGHIVNVAZVOUVMUVQUWI
      UWHUVGWEWFUVMUVQUWJUWHUVGVPWFUVNUVGVQVRZUVKUVNCVKZUUMUVJUVKUWNCUVNUFNZUGU
      HZUVEVCUJZUUTABCDEFGHIVSZVTVAWAWBCUVRWCWDUVMUWBUVIVCUJZUWDUVMUWBUVEUVIUVM
      UWAVLLUWBTLZUVMUWAUVMCTLUVRTLZUWATLABCDEFGHIWGUVMUVOTLZUVGQLZUXAUVMUWKUVG
      TLZUXBUWLUVMUVQUXDUWHUVGWHWFUVNUVGWIWJUVMUVQUXCUWHUVGWKWFUVOUVGWLWJCUVRWM
      VJVOUWAWPWFZUVMUVEQLZUVETLUVMSQLZUVDQLZUXFSWNWOWQZUVMUXGUVCPLZUXHUXIUVMUV
      BOLZUVBPLUXJUVMUWFUVAVDLUXKUWGUUTWRUVAVGVHUVBWSUVBWTVHSUVCXAVJSUVDXBVJUVE
      XQWFUVMUVIQLZUVITLZUVMUULUVHQLZUXLUUKUULUVLXCUVMUVHOLZUXNUVMUVQUUAVDLZUXO
      UWHUUKUXPUULUVLUUAVFXDUVGUUAVIWJUVHWKWFYTUVHXEWJUVIXQWFZUVMUWBUWPUVEVCUVM
      UWAUWOUGUVMUVRUVNCUFUWMXFXGUVKUWQUUMUVJUVKUWNUWQUWRXHVAXIUUMUVKUVJXJXKUVM
      UWTUXMUWSUWDXRUXEUXQUWBUVIXLWJXMUUPUVTUWDUNCUVOYQMNZUDZRZUUCCUXRUFNZUGUHZ
      UIUJZRZUNJKUVOUVGPOYPUVOUDZUUNUXTUUOUYDUYEYSUXSUYEYRUXRCYPUVOYQMXNZXOXPUY
      EUUFUYCUYEUUEUYBUUCUIUYEUUDUYAUGUYEYRUXRCUFUYFXFXGXSXPXTYQUVGUDZUXTUVTUYD
      UWDUYGUXSUVSUYGUXRUVRCYQUVGUVOMYAZXOXPUYGUYCUWCUYGUUCUVIUYBUWBUIUYGUUBUVH
      YTMYQUVGUUAUEXNXFUYGUYAUWAUGUYGUXRUVRCUFUYHXFXGYHXPXTYBYCYDYEYFUURUUHRZJP
      UMUUSUUQUYIJPUUQUUGRZKOUMUYIUUPUYJKOYSUUFYGYIUUGKOYJYKYIUUHJPYJYKWDYLYMEC
      DKJYNYO $.
      $( [20-Nov-2014] $)
  $}


