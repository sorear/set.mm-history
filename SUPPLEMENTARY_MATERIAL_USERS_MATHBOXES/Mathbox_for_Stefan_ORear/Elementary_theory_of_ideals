$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Elementary theory of ideals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LIdeal $.

  $( Ring left-ideal function. $)
  clidl $a class LIdeal $.

  $( Define the class of left ideals of a given ring.  An ideal is a submodule
     of the ring viewed as a module over itself. $)
  df-lidl $a |- LIdeal = ( LSubSp o. ringLMod ) $.

  ${
    $d W a $.
    $( ` ringLMod ` is a function.  (Contributed by Stefan O'Rear,
       6-Dec-2014.) $)
    rlmfn $p |- ringLMod Fn _V $=
      ( va cv cbs cfv csra cvv wcel crglmod wfn df-rgmod fnmpt fvex a1i mprg )
      ABZCDZOEDZDZFGZHFIAFAFRHFAJKSOFGPQLMN $.
      $( [6-Dec-2014] $)

    $( Value of the ring module.  (Contributed by Stefan O'Rear,
       6-Dec-2014.) $)
    rlmval $p |- ( W e. X -> ( ringLMod ` W ) =
        ( ( subringAlg ` W ) ` ( Base ` W ) ) ) $=
      ( va wcel cvv crglmod cfv cbs csra wceq elex fveq2 fveq12d df-rgmod fvmpt
      cv fvex syl ) ABDAEDAFGAHGZAIGZGZJABKCACPZHGZUBIGZGUAEFUBAJUCSUDTUBAILUBA
      HLMCNSTQOR $.
      $( [6-Dec-2014] $)

    $( Value of the set of ring ideals.  (Contributed by Stefan O'Rear,
       6-Dec-2014.) $)
    lidlval $p |- ( W e. X -> ( LIdeal ` W ) =
        ( LSubSp ` ( ringLMod ` W ) ) ) $=
      ( wcel clidl cfv clss crglmod ccom df-lidl fveq1i cvv wfn wceq rlmfn elex
      fvco2 sylancr syl5eq ) ABCZADEAFGHZEZAGEFEZADTIJSGKLAKCUAUBMNABOKFGAPQR
      $.
      $( [6-Dec-2014] $)

    $( Value of the ring span function.  (Contributed by Stefan O'Rear,
       6-Dec-2014.) $)
    rspval $p |- ( W e. X -> ( RSpan ` W ) =
        ( LSpan ` ( ringLMod ` W ) ) ) $=
      ( wcel crsp cfv clspn crglmod ccom df-rsp fveq1i cvv wfn wceq rlmfn fvco2
      elex sylancr syl5eq ) ABCZADEAFGHZEZAGEFEZADTIJSGKLAKCUAUBMNABPKFGAOQR $.
      $( [6-Dec-2014] $)
  $}

  $( Base set of the ring module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlmbase $p |- ( R e. X -> ( Base ` R ) = ( Base ` ( ringLMod ` R ) ) ) $=
    ( wcel crglmod cfv cbs rlmval wss ssid a1i srabase ) ABCZADEAFEZAABGMMHLMIJ
    K $.
    $( [6-Dec-2014] $)

  $( Vector addition in the ring module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlmplusg $p |- ( R e. X -> ( +g ` R ) = ( +g ` ( ringLMod ` R ) ) ) $=
    ( wcel crglmod cfv cbs rlmval wss ssid a1i sraaddg ) ABCZADEAFEZAABGMMHLMIJ
    K $.
    $( [6-Dec-2014] $)

  $( Zero vector in the ring module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlm0 $p |- ( R e. X -> ( 0g ` R ) = ( 0g ` ( ringLMod ` R ) ) ) $=
    ( wcel crglmod cfv cbs c0g rlmval eqidd wss ssid a1i id sralmod0 ) ABCZADEA
    FEZBAAGEZABHOQIPPJOPKLOMN $.
    $( [6-Dec-2014] $)

  $( Scalars in the ring module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlmsca $p |- ( R e. X -> R = ( Scalar ` ( ringLMod ` R ) ) ) $=
    ( wcel cbs cfv cress co crglmod csca eqid ressid rlmval wss ssid a1i srasca
    eqtr3d ) ABCZAADEZFGAAHEZIESABSJKRTSAABLSSMRSNOPQ $.
    $( [6-Dec-2014] $)

  $( Scalar multiplication in the ring module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlmvsca $p |- ( R e. X -> ( .r ` R ) = ( vsca ` ( ringLMod ` R ) ) ) $=
    ( wcel crglmod cfv cbs rlmval wss ssid a1i sravsca ) ABCZADEAFEZAABGMMHLMIJ
    K $.
    $( [6-Dec-2014] $)

  $( The ring module is a module.  (Contributed by Stefan O'Rear,
     6-Dec-2014.) $)
  rlmlmod $p |- ( R e. Ring -> ( ringLMod ` R ) e. LMod ) $=
    ( crg wcel crglmod cfv cbs csra clmod rlmval csubrg subrgid sralmod eqeltrd
    eqid syl ) ABCZADEAFEZAGEEZHABIPQAJECRHCQAQNKRQARNLOM $.
    $( [6-Dec-2014] $)

  ${
    $d x y R $.  $d x y X $.
    $( Vector negation in the ring module.  (Contributed by Stefan O'Rear,
       6-Dec-2014.) $)
    rlmvneg $p |- ( R e. X -> ( invg ` R ) = ( invg ` ( ringLMod ` R ) ) ) $=
      ( vx vy wcel cbs cfv crglmod eqidd rlmbase cv cplusg co wa rlmplusg oveqd
      wceq adantr elex cvv fvex a1i grpinvpropd ) ABEZCDAFGZAAHGZUDUEIABJUDCKZD
      KZALGZMUGUHUFLGZMQUGUEEUHUEENUDUIUJUGUHABOPRABSUFTEUDAHUAUBUC $.
      $( [6-Dec-2014] $)
  $}

  ${
    lidlss.b $e |- B = ( Base ` W ) $.
    lidlss.i $e |- I = ( LIdeal ` W ) $.
    $( An ideal is a subset of the base set.  (Contributed by Stefan O'Rear,
       3-Jan-2015.) $)
    lidlss $p |- ( ( W e. V /\ U e. I ) -> U C_ B ) $=
      ( wcel crglmod cfv cbs clss wss clidl lidlval syl5eq eleq2d biimpa eqid
      wa lssss syl wceq rlmbase adantr sseqtr4d ) EDHZBCHZTZBEIJZKJZAUIBUJLJZHZ
      BUKMUGUHUMUGCULBUGCENJULGEDOPQRULBUKUJUKSULSUAUBUGAUKUCUHUGAEKJUKFEDUDPUE
      UF $.
      $( [3-Jan-2015] $)
  $}

  ${
    lidlcl.u $e |- U = ( LIdeal ` R ) $.

    ${
      lidl0cl.z $e |- Z = ( 0g ` R ) $.
      $( An ideal contains 0.  (Contributed by Stefan O'Rear, 3-Jan-2015.) $)
      lidl0cl $p |- ( ( R e. Ring /\ I e. U ) -> Z e. I ) $=
        ( crg wcel wa crglmod cfv c0g wceq rlm0 syl5eq adantr clss rlmlmod eqid
        clmod clidl lidlval eleq2d biimpa lss0cl syl2anc eqeltrd ) AGHZCBHZIZDA
        JKZLKZCUHDULMUIUHDALKULFAGNOPUJUKTHZCUKQKZHZULCHUHUMUIARPUHUIUOUHBUNCUH
        BAUAKUNEAGUBOUCUDUNCUKULULSUNSUEUFUG $.
        $( [3-Jan-2015] $)
    $}

    ${
      lidlacl.p $e |- P = ( +g ` R ) $.
      $( An ideal is closed under addition.  (Contributed by Stefan O'Rear,
         3-Jan-2015.) $)
      lidlacl $p |- ( ( ( R e. Ring /\ I e. U ) /\ ( X e. I /\ Y e. I ) ) ->
          ( X P Y ) e. I ) $=
        ( crg wcel wa co crglmod cfv cplusg wceq rlmplusg syl5eq oveqd eqid jca
        ad2antrr clmod rlmlmod adantr clidl lidlval eleq2d biimpa lssvacl sylan
        clss eqeltrd ) BIJZDCJZKZEDJFDJKZKEFALZEFBMNZONZLZDUNURVAPUOUQUNAUTEFUN
        ABONUTHBIQRSUBUPUSUCJZDUSULNZJZKUQVADJUPVBVDUNVBUOBUDUEUNUOVDUNCVCDUNCB
        UFNVCGBIUGRUHUIUAUTVCDUSEFUTTVCTUJUKUM $.
        $( [3-Jan-2015] $)
    $}

    ${
      lidlnegcl.n $e |- N = ( invg ` R ) $.
      $( An ideal contains negatives.  (Contributed by Stefan O'Rear,
         3-Jan-2015.) $)
      lidlnegcl $p |- ( ( R e. Ring /\ I e. U /\ X e. I ) ->
          ( N ` X ) e. I ) $=
        ( crg wcel w3a cfv crglmod cminusg rlmvneg syl5eq fveq1d 3ad2ant1 clmod
        wceq eqid rlmlmod clidl lidlval eleq2d biimpa 3adant3 lssvnegcl syl3anc
        clss simp3 eqeltrd ) AHIZCBIZECIZJZEDKZEALKZMKZKZCULUMUPUSSUNULEDURULDA
        MKURGAHNOPQUOUQRIZCUQUIKZIZUNUSCIULUMUTUNAUAQULUMVBUNULUMVBULBVACULBAUB
        KVAFAHUCOUDUEUFULUMUNUJVACURUQEVATURTUGUHUK $.
        $( [3-Jan-2015] $)
    $}

    ${
      lidlcl.b $e |- B = ( Base ` R ) $.

      ${
        lidlmcl.t $e |- T = ( .r ` R ) $.
        $( An ideal is closed under left-multiplication by elements of the full
           ring.  (Contributed by Stefan O'Rear, 3-Jan-2015.) $)
        lidlmcl $p |- ( ( ( R e. Ring /\ I e. U ) /\ ( X e. B /\ Y e. I ) ) ->
            ( X T Y ) e. I ) $=
          ( crg wcel wa co cfv syl5eq ad2antrr cbs eleq2d eqid cvsca wceq cmulr
          crglmod rlmvsca oveqd clmod clss rlmlmod lidlval biimpa adantr rlmsca
          csca clidl fveq2d ad2ant2r simprr lssvscl syl22anc eqeltrd ) BKLZEDLZ
          MZFALZGELZMZMZFGCNZFGBUDOZUAOZNZEVBVIVLUBVCVGVBCVKFGVBCBUCOVKJBKUEPUF
          QVHVJUGLZEVJUHOZLZFVJUNOZROZLZVFVLELVBVMVCVGBUIQVDVOVGVBVCVOVBDVNEVBD
          BUOOVNHBKUJPSUKULVBVEVRVCVFVBVEVRVBAVQFVBABROVQIVBBVPRBKUMUPPSUKUQVDV
          EVFURVQVNVKEVPVJFGVPTVKTVQTVNTUSUTVA $.
          $( [3-Jan-2015] $)
      $}

      ${
        $d B a $.  $d I a $.  $d O a $.  $d R a $.  $d U a $.
        lidl1el.o $e |- O = ( 1r ` R ) $.
        $( An ideal contains 1 iff it is the unit ideal.  (Contributed by
           Stefan O'Rear, 3-Jan-2015.) $)
        lidl1el $p |- ( ( R e. Ring /\ I e. U ) -> ( O e. I <-> I = B ) ) $=
          ( va crg wcel wa wceq wss lidlss adantr cv cmulr cfv co eqid eqeltrrd
          rngridm ad2ant2rl lidlmcl ancom2s expr ssrdv eqssd rngidcl syl5ibrcom
          ex eleq2 impbid ) BJKZDCKZLZEDKZDAMZUQURUSUQURLZDAUQDANURADCJBGFOPUTI
          ADUQURIQZAKZVADKUQURVBLLVAEBRSZTZVADUOVBVDVAMUPURABVCEVAGVCUAZHUCUDUQ
          VBURVDDKABVCCDVAEFGVEUEUFUBUGUHUIULUQURUSEAKZUOVFUPABEGHUJPDAEUMUKUN
          $.
          $( [3-Jan-2015] $)
      $}
    $}
  $}

  ${
    lidl0.u $e |- U = ( LIdeal ` R ) $.
    ${
      lidl0.z $e |- Z = ( 0g ` R ) $.
      $( Every ring contains a zero ideal.  (Contributed by Stefan O'Rear,
         3-Jan-2015.) $)
      lidl0 $p |- ( R e. Ring -> { Z } e. U ) $=
        ( crg wcel csn crglmod cfv c0g clss rlmlmod eqid lsssn0 syl rlm0 syl5eq
        clmod sneqd clidl lidlval eleq12d mpbird ) AFGZCHZBGAIJZKJZHZUGLJZGZUEU
        GSGUKAMUJUGUHUHNUJNOPUEUFUIBUJUECUHUECAKJUHEAFQRTUEBAUAJUJDAFUBRUCUD $.
        $( [3-Jan-2015] $)
    $}

    ${
      lidl1.b $e |- B = ( Base ` R ) $.
      $( Every ring contains a unit ideal.  (Contributed by Stefan O'Rear,
         3-Jan-2015.) $)
      lidl1 $p |- ( R e. Ring -> B e. U ) $=
        ( crg wcel crglmod cfv cbs clss clmod rlmlmod eqid rlmbase syl5eq clidl
        lss1 syl lidlval eleq12d mpbird ) BFGZACGBHIZJIZUDKIZGZUCUDLGUGBMUFUEUD
        UENUFNRSUCAUECUFUCABJIUEEBFOPUCCBQIUFDBFTPUAUB $.
        $( [3-Jan-2015] $)
    $}
  $}

  ${
    rspcl.k $e |- K = ( RSpan ` R ) $.
    ${
      rspcl.b $e |- B = ( Base ` R ) $.
      ${
        rspcl.u $e |- U = ( LIdeal ` R ) $.
        $( The span of a set of ring elements is an ideal.  (Contributed by
           Stefan O'Rear, 3-Jan-2015.) $)
        rspcl $p |- ( ( R e. Ring /\ G C_ B ) -> ( K ` G ) e. U ) $=
          ( crg wcel wss wa cfv crglmod clspn cbs adantr syl5eq eqid wceq clmod
          clss rlmlmod rlmbase sseq2d biimpa syl2anc crsp rspval fveq1d lidlval
          lspcl clidl eleq12d mpbird ) BIJZDAKZLZDEMZCJDBNMZOMZMZUTUBMZJZURUTUA
          JZDUTPMZKZVDUPVEUQBUCQUPUQVGUPAVFDUPABPMVFGBIUDRUEUFVCDVAVFUTVFSVCSVA
          SULUGURUSVBCVCUPUSVBTUQUPDEVAUPEBUHMVAFBIUIRUJQUPCVCTUQUPCBUMMVCHBIUK
          RQUNUO $.
          $( [3-Jan-2015] $)
      $}

      $( The span of a set of ring elements contains those elements.
         (Contributed by Stefan O'Rear, 3-Jan-2015.) $)
      rspssid $p |- ( ( R e. Ring /\ G C_ B ) -> G C_ ( K ` G ) ) $=
        ( crg wcel wss cfv crglmod clspn clmod cbs rlmlmod adantr syl5eq sseq2d
        wa eqid rlmbase biimpa lspssid syl2anc wb crsp rspval fveq1d mpbird ) B
        GHZCAIZSZCCDJZIZCCBKJZLJZJZIZULUOMHZCUONJZIZURUJUSUKBOPUJUKVAUJAUTCUJAB
        NJUTFBGUAQRUBCUPUTUOUTTUPTUCUDUJUNURUEUKUJUMUQCUJCDUPUJDBUFJUPEBGUGQUHR
        PUI $.
        $( [3-Jan-2015] $)

      ${
        rsp1.o $e |- O = ( 1r ` R ) $.
        $( The span of the identity element is the unit ideal.  (Contributed by
           Stefan O'Rear, 3-Jan-2015.) $)
        rsp1 $p |- ( R e. Ring -> ( K ` { O } ) = B ) $=
          ( crg wcel csn cfv wceq wss rngidcl snssd rspssid mpdan cur cvv fvex
          eqeltri snss sylibr clidl wb eqid rspcl lidl1el mpbid ) BHIZDDJZCKZIZ
          ULALZUJUKULMZUMUJUKAMZUOUJDAABDFGNOZABUKCEFPQDULDBRKSGBRTUAUBUCUJULBU
          DKZIZUMUNUEUJUPUSUQABURUKCEFURUFZUGQABURULDUTFGUHQUI $.
          $( [3-Jan-2015] $)
      $}
    $}

    ${
      rsp0.z $e |- Z = ( 0g ` R ) $.
      $( The span of the zero element is the zero ideal.  (Contributed by
         Stefan O'Rear, 3-Jan-2015.) $)
      rsp0 $p |- ( R e. Ring -> ( K ` { Z } ) = { Z } ) $=
        ( crg wcel csn cfv wceq crglmod c0g clspn clmod rlmlmod eqid lspsn0 syl
        crsp syl5eq rspval rlm0 sneqd fveq12d eqeq12d mpbird ) AFGZCHZBIZUHJAKI
        ZLIZHZUJMIZIZULJZUGUJNGUOAOUMUJUKUKPUMPQRUGUIUNUHULUGUHULBUMUGBASIUMDAF
        UATUGCUKUGCALIUKEAFUBTUCZUDUPUEUF $.
        $( [3-Jan-2015] $)
    $}

    ${
      rspssp.u $e |- U = ( LIdeal ` R ) $.
      $( PLEASE PUT DESCRIPTION HERE. (Contributed by Stefan O'Rear,
         3-Jan-2015.) $)
      rspssp $p |- ( ( R e. Ring /\ I e. U /\ G C_ I ) -> ( K ` G ) C_ I ) $=
        ( crg wcel wss w3a crglmod clspn wceq crsp rspval syl5eq 3ad2ant1 eqid
        cfv fveq1d clmod clss rlmlmod clidl lidlval eleq2d biimpa 3adant3 simp3
        lspssp syl3anc eqsstrd ) AHIZDBIZCDJZKZCETZCALTZMTZTZDUNUOURVANUPUNCEUT
        UNEAOTUTFAHPQUARUQUSUBIZDUSUCTZIZUPVADJUNUOVBUPAUDRUNUOVDUPUNUOVDUNBVCD
        UNBAUETVCGAHUFQUGUHUIUNUOUPUJVCCDUTUSVCSUTSUKULUM $.
        $( [3-Jan-2015] $)
    $}
  $}

  ${
    $d I x $.  $d Z x $.
    lidlnz.u $e |- U = ( LIdeal ` R ) $.
    lidlnz.z $e |- Z = ( 0g ` R ) $.
    $( A nonzero ideal contains a nonzero element.  (Contributed by Stefan
       O'Rear, 3-Jan-2015.) $)
    lidlnz $p |- ( ( R e. Ring /\ I e. U /\ I =/= { Z } ) ->
        E. x e. I x =/= Z ) $=
      ( crg wcel csn wne w3a cv wn wa wex wrex wpss wss lidl0cl necomd sylanbrc
      snssd 3adant3 simp3 df-pss pssnel syl elsn necon3bbii anbi2i exbii df-rex
      bitr4i sylib ) BHIZDCIZDEJZKZLZAMZDIZVAURIZNZOZAPZVAEKZADQZUTURDRZVFUTURD
      SZURDKVIUPUQVJUSUPUQOEDBCDEFGTUCUDUTDURUPUQUSUEUAURDUFUBAURDUGUHVFVBVGOZA
      PVHVEVKAVDVGVBVCVAEAEUIUJUKULVGADUMUNUO $.
      $( [3-Jan-2015] $)
  $}

  ${
    $d U a b $.  $d R a b $.  $d B a b $.  $d Z a b $.
    drngnidl.b $e |- B = ( Base ` R ) $.
    drngnidl.z $e |- Z = ( 0g ` R ) $.
    drngnidl.u $e |- U = ( LIdeal ` R ) $.
    $( A division ring has only the two trivial ideals.  (Contributed by Stefan
       O'Rear, 3-Jan-2015.) $)
    drngnidl $p |- ( R e. DivRing -> U = { { Z } , B } ) $=
      ( va vb cdr wcel cv wa wceq simpr wne cfv ad2antrr syl3anc eqid orcd wrex
      csn cpr wo cur crg drngrng simplr lidlnz wel cinvr cmulr co simpll lidlss
      sselda adantrr simprr drnginvrl drnginvrcl simprl syl22anc eqeltrrd exp32
      lidlmcl rexlimdv imp syldan wb lidl1el sylan adantr mpbid olcd pm2.61dane
      vex elpr sylibr ex ssrdv wss lidl0 lidl1 snex cbs cvv fvex eqeltri bicomi
      prss sylanbrc syl eqssd ) BJKZCDUCZAUDZWOHCWQWOHLZCKZWRWQKZWOWSMZWRWPNZWR
      ANZUEZWTXAXDWRWPXAXBMXBXCXAXBOUAXAWRWPPZMZXCXBXFBUFQZWRKZXCXAXEILZDPZIWRU
      BZXHXFBUGKZWSXEXKWOXLWSXEBUHZRWOWSXEUIXAXEOIBCWRDGFUJSXAXKXHXAXJXHIWRXAIH
      UKZXJXHXAXNXJMZMZXIBULQZQZXIBUMQZUNZXGWRXPWOXIAKZXJXTXGNWOWSXOUOZXAXNYAXJ
      XAWRAXIAWRCJBEGUPUQURZXAXNXJUSZABXSXGXQXIDEFXSTZXGTZXQTZUTSXPXLWSXRAKZXNX
      TWRKWOXLWSXOXMRWOWSXOUIXPWOYAXJYHYBYCYDABXQXIDEFYGVASXAXNXJVBABXSCWRXRXIG
      EYEVFVCVDVEVGVHVIXAXHXCVJZXEWOXLWSYIXMABCWRXGGEYFVKVLVMVNVOVPWRWPAHVQVRVS
      VTWAWOXLWQCWBZXMXLWPCKZACKZYJBCDGFWCABCGEWDYKYLMYJWPACDWEABWFQWGEBWFWHWIW
      KWJWLWMWN $.
      $( [3-Jan-2015] $)
  $}

