$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Subset order structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c toInc $.

  $( Class function defining inclusion posets. $)
  cipo $a class toInc $.

  ${
    $d f x y $.
    $( For any family of sets, define the poset of that family ordered by
       inclusion.  See ~ ipobas , ~ ipolerval , and ~ ipole for its contract.

       _EDITORIAL_:  I'm not thrilled with the name.  Any suggestions?
       Possible future directions include adding relative complement for the
       ` oc ` slot, or the order topology. $)
    df-ipo $a |- toInc = ( f e. _V |-> { <. ( Base ` ndx ) , f >. ,
        <. ( le ` ndx ) , { <. x , y >. |
            ( { x , y } C_ f /\ x C_ y ) } >. } ) $.
  $}

  ${
    $d F f x y $.  $d I f x y $.  $d V f x y $.
    ipoval.i $e |- I = ( toInc ` F ) $.
    $( Value of the inclusion poset.  (Contributed by Stefan O'Rear,
       30-Jan-2015.) $)
    ipoval $p |- ( F e. V -> I = { <. ( Base ` ndx ) , F >. , <. ( le ` ndx ) ,
          { <. x , y >. | ( { x , y } C_ F /\ x C_ y ) } >. } ) $=
      ( vf wcel cvv cnx cbs cfv cop cv cpr wss wa copab wceq cipo cple opabbidv
      elex opeq2 sseq2 anbi1d opeq2d preq12d df-ipo prex fvmpt syl5eq syl ) CEH
      CIHZDJKLZCMZJUALZANZBNZOZCPZURUSPZQZABRZMZOZSCEUCUNDCTLVFFGCUOGNZMZUQUTVG
      PZVBQZABRZMZOVFITVGCSZVHUPVLVEVGCUOUDVMVKVDUQVMVJVCABVMVIVAVBVGCUTUEUFUBU
      GUHABGUIUPVEUJUKULUM $.
      $( [30-Jan-2015] $)

    $( Base set of the inclusion poset.  (Contributed by Stefan O'Rear,
       30-Jan-2015.) $)
    ipobas $p |- ( F e. V -> F = ( Base ` I ) ) $=
      ( vx vy wcel cnx cbs cfv cop cple cv cpr wss wa copab eqid posbase ipoval
      fveq2d eqtr4d ) ACGZAHIJAKHLJEMZFMZNAOUDUEOPEFQZKNZIJBIJAUGUFCUGRSUCBUGIE
      FABCDTUAUB $.
      $( [30-Jan-2015] $)

    $( Relation of the inclusion poset.  (Contributed by Stefan O'Rear,
       30-Jan-2015.) $)
    ipolerval $p |- ( F e. V ->
          { <. x , y >. | ( { x , y } C_ F /\ x C_ y ) } = ( le ` I ) ) $=
      ( wcel cv cpr wss wa copab cnx cbs cfv cop cple cvv wceq vex prss biimpri
      cxp adantr ssopab2i df-xp sseqtr4i xpexg anidms ssexg sylancr eqid ipoval
      posle syl fveq2d eqtr4d ) CEGZAHZBHZICJZUSUTJZKZABLZMNOCPMQOVDPIZQOZDQOUR
      VDRGZVDVFSURVDCCUCZJVHRGZVGVDUSCGUTCGKZABLVHVCVJABVAVJVBVJVAUSUTCATBTUAUB
      UDUEABCCUFUGURVICCEEUHUIVDVHRUJUKCVEVDRVEULUNUOURDVEQABCDEFUMUPUQ $.
      $( [30-Jan-2015] $)

    ipole.l $e |- L = ( le ` I ) $.
    $d L x y $.  $d X x y $.  $d Y x y $.

    $( Weak order condition of the inclusion poset.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    ipole $p |- ( ( F e. V /\ X e. F /\ Y e. F ) -> ( X L Y <-> X C_ Y ) ) $=
      ( vx vy wcel w3a cv cpr wss wa wbr wb wceq 3adant1 copab preq12 eqid cple
      sseq1d sseq12 anbi12d brabga cfv ipolerval syl6reqr breqd prssi biantrurd
      3ad2ant1 3bitr4d ) ADKZEAKZFAKZLZEFIMZJMZNZAOZVAVBOZPZIJUAZQZEFNZAOZEFOZP
      ZEFCQZVKURUSVHVLRUQVFVLIJEFVGAAVAESVBFSPZVDVJVEVKVNVCVIAVAVBEFUBUEVAEVBFU
      FUGVGUCUHTUQURVMVHRUSUQCVGEFUQVGBUDUICIJABDGUJHUKULUOUTVJVKURUSVJUQEFAUMT
      UNUP $.
      $( [30-Jan-2015] $)
  $}

  ${
    ipolt.i $e |- I = ( toInc ` F ) $.
    ipolt.l $e |- S = ( lt ` I ) $.
    $( Strict order condition of the inclusion poset.  (Contributed by Stefan
       O'Rear, 30-Jan-2015.) $)
    ipolt $p |- ( ( F e. V /\ X e. F /\ Y e. F ) -> ( X S Y <-> X C. Y ) ) $=
      ( wcel w3a cple cfv wbr wne wa wss wpss wb cvv cipo anbi1d eqeltri pltval
      eqid ipole fvex mp3an1 3adant1 df-pss a1i 3bitr4d ) BDIZEBIZFBIZJZEFCKLZM
      ZEFNZOZEFPZUROZEFAMZEFQZUOUQUTURBCUPDEFGUPUDZUEUAUMUNVBUSRZULCSIUMUNVECBT
      LSGBTUFUBSBBACUPEFVDHUCUGUHVCVARUOEFUIUJUK $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d a b c $.
    $( Technical lemma to simplify the statement of ~ ipopos .  The empty set
       is (rather pathologically) a poset under our definitions, since it has
       an empty base set ( ~ str0 ) and any relation partially orders an empty
       set.  (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    0pos $p |- (/) e. Poset $=
      ( va vb vc c0 cpo wcel cvv cv cple cfv wbr wa weq wi w3a wral 0ex cbs cnx
      ral0 baseid str0 eqid ispos mpbir2an ) DEFDGFAHZUFDIJZKUFBHZUGKZUHUFUGKLA
      BMNUIUHCHZUGKLUFUJUGKNOCDPBDPZADPQUKATABCDDUGRSRJAAUAUBUGUCUDUE $.
      $( [30-Jan-2015] $)
  $}

  ${
    $d F a b c $.  $d I a b c $.
    ipopos.i $e |- I = ( toInc ` F ) $.
    $( The inclusion poset on a family of sets is actually a poset.
       (Contributed by Stefan O'Rear, 30-Jan-2015.) $)
    ipopos $p |- I e. Poset $=
      ( va vb vc cvv wcel cpo cfv cipo a1i cv wa wbr wss wb ipole w3a anbi12d
      cple fvex eqeltri ipobas eqidd ssid eqid 3anidm23 mpbiri weq 3com23 simpl
      simpr eqssd syl6bi wi sstr 3adant3r3 3adant3r1 3adant3r2 3imtr4d wn fvprc
      isposd c0 syl5eq 0pos syl6eqel pm2.61i ) AGHZBIHVJDEFABBUAJZBGHVJBAKJZGCA
      KUBUCLABGCUDVJVKUEVJDMZAHZNVMVMVKOZVMVMPZVMUFVJVNVOVPQABVKGVMVMCVKUGZRUHU
      IVJVNEMZAHZSZVMVRVKOZVRVMVKOZNVMVRPZVRVMPZNZDEUJVTWAWCWBWDABVKGVMVRCVQRZV
      JVSVNWBWDQABVKGVRVMCVQRUKTWEVMVRWCWDULWCWDUMUNUOVJVNVSFMZAHZSNZWCVRWGPZNZ
      VMWGPZWAVRWGVKOZNVMWGVKOZWKWLUPWIVMVRWGUQLWIWAWCWMWJVJVNVSWAWCQWHWFURVJVS
      WHWMWJQVNABVKGVRWGCVQRUSTVJVNWHWNWLQVSABVKGVMWGCVQRUTVAVDVJVBZBVEIWOBVLVE
      CAKVCVFVGVHVI $.
      $( [30-Jan-2015] $)
  $}


