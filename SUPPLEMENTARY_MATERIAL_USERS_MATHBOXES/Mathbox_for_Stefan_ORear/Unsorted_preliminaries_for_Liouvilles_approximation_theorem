$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Unsorted preliminaries for Liouville's approximation theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F a $.
    $( Real-coefficient polynomials restrict to real functions.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    plyreres $p |- ( F e. ( Poly ` RR ) -> ( F |` RR ) : RR --> RR ) $=
      ( va cr cply cfv wcel cres wfn crn wss wf cc plybss wb plyf mpbird adantl
      ffn wceq ccj fnssresb 3syl cv wral fvres recn plyrecj sylan2 fveq2d eqtrd
      cjre ffvelrn syl2an cjreb syl eqeltrd ralrimiva fnfvrnss syl2anc sylanbrc
      wa df-f ) ACDEFZACGZCHZVDICJZCCVDKVCVECLJZCAMVCLLAKZALHVEVGNCAOZLLARLCAUA
      UBPZVCVEBUCZVDEZCFZBCUDVFVJVCVMBCVCVKCFZVAZVLVKAEZCVNVLVPSVCVKCAUEQVOVPCF
      ZVPTEZVPSZVOVRVKTEZAEZVPVNVCVKLFZVRWASVKUFZVKAUGUHVOVTVKAVNVTVKSVCVKUKQUI
      UJVOVPLFZVQVSNVCVHWBWDVNVIWCLLVKAULUMVPUNUOPUPUQBCCVDURUSCCVDVBUT $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph z j k $.  $d A z j k $.  $d B z $.  $d N j k z $.
    dvply1.f $e |- ( ph -> F = ( z e. CC |-> sum_ k e. ( 0 ... N )
        ( ( A ` k ) x. ( z ^ k ) ) ) ) $.
    dvply1.g $e |- ( ph -> G = ( z e. CC |-> sum_ k e. ( 0 ... ( N - 1 ) )
        ( ( B ` k ) x. ( z ^ k ) ) ) ) $.
    dvply1.a $e |- ( ph -> A : NN0 --> CC ) $.
    dvply1.b $e |- B = ( k e. NN0 |-> ( ( k + 1 ) x. ( A ` ( k + 1 ) ) ) ) $.
    dvply1.n $e |- ( ph -> N e. NN0 ) $.
    $( Derivative of a polynomial, explicit sum version.  (Contributed by
       Stefan O'Rear, 13-Nov-2014.)  (Revised by Mario Carneiro,
       11-Feb-2015.) $)
    dvply1 $p |- ( ph -> ( CC _D F ) = G ) $=
      ( cc co cc0 cmul c1 wcel cn0 vj cdv cfz cv cfv cexp cmpt wceq cmin oveq2d
      csu cif cabs ccom copn cop csubsp ctop cme eqid cnmet opntop cuni cnmetba
      ax-mp uniopn2 subspid eqcomi cr cpr cnex prid2 a1i topopn syl fzfid wa wf
      elfznn0 ffvelrn syl2an adantr simpr expcl syl2anc mulcl 3impa w3a 3adant3
      ad2antlr 0cnALT wn simpl2 nn0cn 3syl simpl3 cn wo elnn0 biimpi orel2 sylc
      nnm1nn0 ifclda cvv wss ssid cz 0z elexi ovex ifex adantl dvexp2 dvmptcmul
      dvmptfsum wne elfznn nnne0 notnot2 necon1ai iffalse sumeq2dv 1nn0 eleqtri
      cuz nn0uz fzss1 sylancr nn0sscn sseldi simplr caddc ax-1cn oveq1i sylancl
      ad2antrr oveq1d oveq1 oveq12d nnnn0 eqeltrd eldifn addid2i eleq2i sylnibr
      df-ne cdif eldifi syl6eleq elfzp12 mpbid iftrue mul01 sylan2 eqtrd fsumss
      pncan peano2nn0 mulass syl3anc mulcom 3eqtr2d subidi sumeq1i fveq2d oveq2
      wb cbvsumv 3eqtr4g 1z fveq2 id fsumshftm fvmpt2 3eqtr4d 3eqtr3d mpteq2dva
      nn0z eqtr4d 3eqtrd ) ANFUBONBNPHUCOZEUDZCUEZBUDZUWCUFOZQOZEUKUGZUBOBNUWBU
      WDUWCPUHZPUWCUWEUWCRUIOZUFOZQOZULZQOZEUKZUGZGAFUWHNUBIUJABUWGUWNNEUWBUMUI
      UNZUOUEZUWRNNUWRUPUQUEZUWRUWRURSZUWSUWRUHUWQUSSZUWTUWQUWQUTZVAZUWQUWRUWRU
      TZVBVEZUWRNUXANUWRVCUHUXCUWQUWRNUWQUXBVDUXDVFVEZVGVEVHUXDNVINVJSZAVINVKVL
      ZVMAUWTNUWRSUWTAUXEVMUWRNUXFVNVOAPHVPAUWCUWBSZUWENSZUWGNSZAUXIVQZUXJVQZUW
      DNSZUWFNSZUXKUXLUXNUXJATNCVRZUWCTSZUXNUXIKUWCHVSZTNUWCCVTZWAZWBUXMUXJUXQU
      XOUXLUXJWCUXIUXQAUXJUXRWJUWEUWCWDWEZUWDUWFWFWEWGAUXIUXJWHZUXNUWMNSZUWNNSZ
      AUXIUXNUXJUXTWIUYBUWIPUWLNPNSUYBUWIVQWKVMUYBUWIWLZVQZUWCNSZUWKNSZUWLNSZUY
      FUXIUXQUYGAUXIUXJUYEWMZUXRUWCWNWOUYFUXJUWJTSZUYHAUXIUXJUYEWPUYFUWCWQSZUYK
      UYFUYEUYLUWIWRZUYLUYBUYEWCUYFUXIUXQUYMUYJUXRUXQUYMUWCWSWTWOUWIUYLXAXBUWCX
      CZVOUWEUWJWDZWEUWCUWKWFZWEXDUWDUWMWFZWEUXLBUWFUWMUWDNXENUXGUXLUXHVMNNXFUX
      LNXGVMUYAUWMXESUXMUWIPUWLPXHXIXJUWCUWKQXKXLVMUXLUXQNBNUWFUGUBOBNUWMUGUHUX
      IUXQAUXRXMBUWCXNVOUXTXOXPAUWPBNPHRUIOZUCOZUWCDUEZUWFQOZEUKZUGGABNUWOVUBAU
      XJVQZRHUCOZUWNEUKVUDUWDUWLQOZEUKZUWOVUBVUCVUDUWNVUEEVUCUWCVUDSZVQZUWMUWLU
      WDQVUHUYEUWMUWLUHZVUGUYEVUCVUGUYLUWCPXQZUYEUWCHXRZUWCXSZUYEUWCPUWIXTYAWOX
      MUWIPUWLYBZVOUJYCVUCVUDUWBUWNEVUCRPYFUEZSHTSZVUDUWBXFRTVUNYDYGYEAVUOUXJMW
      BZRPHTYHYIVUHUXNUYCUYDVUCUXPUXQUXNVUGAUXPUXJKWBZVUGUYLUXQVUKUWCUUAVOZUXSW
      AZVUHUWMUWLNVUHVUJUYEVUIVUGVUJVUCVUGUYLVUJVUKVULVOXMVUJUYEUWCPUUGWTVUMWOV
      UHUYGUYHUYIVUHTNUWCYJVUGUXQVUCVURXMYKVUHUXJUYKUYHAUXJVUGYLVUGUYKVUCVUGUYL
      UYKVUKUYNVOXMUYOWEUYPWEZUUBUYQWEVUCUWCUWBVUDUUHSZVQZUWNUWDPQOZPVVBUWMPUWD
      QVVBUWIUWMPUHVVBUWCPRYMOZHUCOZSZWLZUWIVVFWRZUWIVVAVVGVUCVVAVUGVVFUWCUWBVU
      DUUCVVEVUDUWCVVDRHUCRYNUUDYOUUEUUFXMVVBUXIVVHVVAUXIVUCUWCUWBVUDUUIZXMVVBH
      VUNSZUXIVVHUVHAVVJUXJVVAAHTVUNMYGUUJYQUWCPHUUKVOUULVVFUWIXAXBUWIPUWLUUMVO
      UJVVAVUCUXIVVCPUHZVVIVUCUXIVQUXNVVKVUCUXPUXQUXNUXIVUQUXRUXSWAUWDUUNVOUUOU
      UPVUCPHVPUUQVUCRRUIOZUYRUCOZUAUDZRYMOZCUEZVVOUWEVVORUIOZUFOZQOZQOZUAUKZUY
      SUWCRYMOZVWBCUEZQOZUWFQOZEUKZVUFVUBVUCUYSVVTUAUKUYSVVOVVPQOZUWEVVNUFOZQOZ
      UAUKVWAVWFVUCUYSVVTVWIUAVUCVVNUYSSZVQZVVTVVPVVOVWHQOZQOZVVPVVOQOZVWHQOZVW
      IVWKVVSVWLVVPQVWKVVRVWHVVOQVWKVVQVVNUWEUFVWKVVNNSRNSVVQVVNUHVWKTNVVNYJVWJ
      VVNTSZVUCVVNUYRVSZXMZYKYNVVNRUURYPUJUJUJVWKVVPNSZVVONSZVWHNSZVWOVWMUHVWKU
      XPVVOTSZVWSAUXPUXJVWJKYQVWJVXBVUCVWJVWPVXBVWQVVNUUSVOXMZTNVVOCVTWEZVWKTNV
      VOYJVXCYKZVWKUXJVWPVXAAUXJVWJYLVWRUWEVVNWDWEVVPVVOVWHUUTUVAVWKVWNVWGVWHQV
      WKVWSVWTVWNVWGUHVXDVXEVVPVVOUVBWEYRUVCYCVVMUYSVVTUAVVLPUYRUCRYNUVDYOUVEUY
      SVWEVWIEUAUWCVVNUHZVWDVWGUWFVWHQVXFVWBVVOVWCVVPQUWCVVNRYMYSZVXFVWBVVOCVXG
      UVFYTUWCVVNUWEUFUVGYTUVIUVJVUCVUEVVTEUARRHRXHSVUCUVKVMZVXHVUCVUOHXHSVUPHU
      VSVOVUHUXNUYIVUENSVUSVUTUWDUWLWFWEUWCVVOUHZUWDVVPUWLVVSQUWCVVOCUVLVXIUWCV
      VOUWKVVRQVXIUVMVXIUWJVVQUWEUFUWCVVORUIYSUJYTYTUVNVUCUYSVUAVWEEVUCUWCUYSSZ
      VQZUYTVWDUWFQVXKUXQVWDXESUYTVWDUHVXJUXQVUCUWCUYRVSXMVWBVWCQXKETVWDXEDLUVO
      YPYRYCUVPUVQUVRJUVTUWA $.
      $( [11-Feb-2015] $) $( [13-Nov-2014] $)
  $}

  ${
    $d S a b c p $.  $d F a b c p $.  $d D a b c p $.

    $( An infinite set of values can be extended to a polynomial in at most one
       way.  (Contributed by Stefan O'Rear, 14-Nov-2014.) $)
    plyexmo $p |- ( ( D C_ CC /\ -. D e. Fin ) -> E* p ( p e. ( Poly ` S ) /\
        ( p |` D ) = F ) ) $=
      ( va cc wcel wa cv cfv wceq cmin cc0 c0p wfn cvv wf syl adantr syl2anc vb
      wss cfn wn cply cres wmo weq wi wal cof co csn cxp ccnv cima simplr sseld
      simpll simprll plyf ffn simprrl a1i sselda fnfvof syl22anc simprlr eqtr4d
      cnex simprrr fveq1d fvres adantl 3eqtr3d ffvelrn subeq0 mpbird eqtrd jcad
      wb ex plysubcl 0cn elexi fniniseg sylancl sylibrd ssrdv ssfi expcom chash
      mtod cdgr cle wbr df-ne biimpri eqid fta1 syl2an simpld mt3d df-0p syl6eq
      ofsubeq0 syl3anc mpbid alrimivv eleq1 reseq1 eqeq1d anbi12d sylibr plyssc
      wne mo4 sseli anim1i immoi ) AFUBZAUCGZUDZHZDIZFUEJZGZYEAUFZCKZHZDUGZYEBU
      EJZGZYIHZDUGYDYJEIZYFGZYOAUFZCKZHZHZDEUHZUIZEUJDUJYKYDUUBDEYDYTUUAYDYTHZY
      EYOLUKULZFMUMZUNZKZUUAUUCUUDNUUFUUCUUDNKZUUDUOUUEUPZUCGZUUCUUJYBYAYCYTUQU
      UCAUUIUBZUUJYBUIUUCUAAUUIUUCUAIZAGZUULFGZUULUUDJZMKZHZUULUUIGZUUCUUMUUNUU
      PUUCAFUULYAYCYTUSZURUUCUUMUUPUUCUUMHZUUOUULYEJZUULYOJZLULZMUUTYEFOZYOFOZF
      PGZUUNUUOUVCKUUCUVDUUMUUCFFYEQZUVDUUCYGUVGYDYGYIYSUTZFYEVARZFFYEVBRSUUCUV
      EUUMUUCFFYOQZUVEUUCYPUVJYDYJYPYRVCZFYOVARZFFYOVBRSUVFUUTVJVDUUCAFUULUUSVE
      ZFLYEYOPUULVFVGUUTUVCMKZUVAUVBKZUUTUULYHJZUULYQJZUVAUVBUUTUULYHYQUUCYHYQK
      UUMUUCYHCYQYDYGYIYSVHYDYJYPYRVKVISVLUUMUVPUVAKUUCUULAYEVMVNUUMUVQUVBKUUCU
      ULAYOVMVNVOUUTUVAFGZUVBFGZUVNUVOWAUUTUVGUUNUVRUUCUVGUUMUVISUVMFFUULYEVPTU
      UTUVJUUNUVSUUCUVJUUMUVLSUVMFFUULYOVPTUVAUVBVQTVRVSWBVTUUCUUDFOZMPGUURUUQW
      AUUCFFUUDQZUVTUUCUUDYFGZUWAUUCYGYPUWBUVHUVKFYEYOWCTZFUUDVARFFUUDVBRMFWDWE
      FMUULUUDPWFWGWHWIUUJUUKYBUUIAWJWKRWMUUCUUHUDZUUJUUCUWDHUUJUUIWLJUUDWNJWOW
      PZUUCUWBUUDNXPZUUJUWEHUWDUWCUWFUWDUUDNWQWRUUIFUUDUUIWSWTXAXBWBXCXDXEUUCUV
      FUVGUVJUUGUUAWAUVFUUCVJVDUVIUVLFYEYOPXFXGXHWBXIYJYSDEUUAYGYPYIYRYEYOYFXJU
      UAYHYQCYEYOAXKXLXMXQXNYNYJDYMYGYIYLYFYEBXOXRXSXTR $.
      $( [14-Nov-2014] $)
  $}

  ${
    $d a b c F $.  $d a b c S $.
    $( The derivative of a polynomial is a polynomial.  (Contributed by Stefan
       O'Rear, 14-Nov-2014.)  (The proof was shortened by Mario Carneiro,
       11-Feb-2015.) $)
    dvply2 $p |- ( F e. ( Poly ` S ) -> ( CC _D F ) e. ( Poly ` CC ) ) $=
      ( va vb vc cfv wcel cc co cc0 cfz cv cn0 c1 caddc cmul cmpt csu wceq syl
      cply cdv cdgr ccoe cexp plyf feqmptd cuz wa cz dgrcl nn0z adantr peano2uz
      uzid 3syl eqid coeid3 3com23 mpd3an3 mpteq2dva eqtrd nn0cn ax-1cn sylancl
      pncan eqcomd oveq2d sumeq1d mpteq2dv coef3 oveq1 fveq2d oveq12d peano2nn0
      cmin cbvmptv dvply1 wss wf ssid adantl ffvelrn syl2anc mulcl fmptd elplyr
      a1i syl3anc eqeltrd ) BAUAFGZHBUBICHJBUCFZKIZDLZEMELZNOIZWPBUDFZFZPIZQZFC
      LZWNUEIZPIZDRZQZHUAFZWKCWQWTDBXEWLNOIZWKBCHXABFZQCHJXGKIWNWQFXBPIDRZQWKCH
      HBABUFUGWKCHXHXIWKXAHGZXGWLUHFZGZXHXISZWKXJUIWLUJGZWLXKGXLWKXNXJWKWLMGZXN
      ABUKZWLULTUMWLUOWLWLUNUPWKXLXJXMWQADBXGWLXAWQUQZWLUQURUSUTVAVBWKCHXDJXGNV
      PIZKIZXCDRWKWMXSXCDWKWLXRJKWKXRWLWKWLHGZNHGXRWLSWKXOXTXPWLVCTVDWLNVFVEVGV
      HVIVJWQABXQVKZEDMWSWNNOIZYBWQFZPIWOWNSZWPYBWRYCPWOWNNOVLZYDWPYBWQYEVMVNVQ
      WKXOXGMGXPWLVOTVRWKHHVSZXOMHWTVTXEXFGYFWKHWAWHXPWKEMWSHWTWKWOMGZUIZWPHGZW
      RHGZWSHGYHWPMGZYIYGYKWKWOVOWBZWPVCTYHMHWQVTZYKYJWKYMYGYAUMYLMHWPWQWCWDWPW
      RWEWDWTUQWFCWTHDWLWGWIWJ $.
      $( [11-Feb-2015] $) $( [14-Nov-2014] $)
  $}

  ${
    $d n x F $.  $d x N $.  $d n x S $.
    $( Polynomials have polynomials as derivatives of all orders.  (Contributed
       by Stefan O'Rear, 15-Nov-2014.)  (The proof was shortened by Mario
       Carneiro, 11-Feb-2015.) $)
    dvnply $p |- ( ( F e. ( Poly ` S ) /\ N e. NN0 ) ->
      ( ( CC Dn F ) ` N ) e. ( Poly ` CC ) ) $=
      ( vx vn cn0 wcel cply cfv cc co cv cc0 wceq fveq2 eleq1d imbi2d a1i cvv
      wi cdvn c1 caddc wss cpm ssid wf cnex plyf fpmg syl3anc dvn0 plyssc sseli
      syl2anc eqeltrd wa dvply2 dvnp1 mp3an1 sylan syl5ibr expcom nn0ind impcom
      cdv a2d ) CFGBAHIZGZCJBUAKZIZJHIZGZVIDLZVJIZVLGZTVIMVJIZVLGZTVIELZVJIZVLG
      ZTVIVSUBUCKZVJIZVLGZTVIVMTDECVNMNZVPVRVIWEVOVQVLVNMVJOPQVNVSNZVPWAVIWFVOV
      TVLVNVSVJOPQVNWBNZVPWDVIWGVOWCVLVNWBVJOPQVNCNZVPVMVIWHVOVKVLVNCVJOPQVIVQB
      VLVIJJUDZBJJUEKGZVQBNWIVIJUFZRVIJSGZWLJJBUGWJWLVIUHRZWMABUIJJBSSUJUKZJBUL
      UOVHVLBAUMUNUPVSFGZVIWAWDVIWOWAWDTWAWDVIWOUQZJVTVFKZVLGJVTURWPWCWQVLVIWJW
      OWCWQNZWNWIWJWOWRWKJBVSUSUTVAPVBVCVGVDVE $.
      $( [11-Feb-2015] $) $( [15-Nov-2014] $)

    $( Polynomials are smooth.  (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    plycpn $p |- ( F e. ( Poly ` S ) -> F e. |^| ran ( C^n ` CC ) ) $=
      ( vx vn cply cfv wcel cc ccpn crn cv wral cn0 wa co ccncf cnex syl ax-mp
      wb cint cpm cdvn cdm plyf adantr cvv idi fpm dvnply plycn wceq fdm oveq1d
      wf eleqtrrd wss a1i elcpn sylan mpbir2and ralrimiva wfn fncpn eleq2 ralrn
      ssid sylibr elintg mpbird ) BAEFZGZBHIFZJZUAGBCKZGZCVNLZVLBDKZVMFZGZDMLZV
      QVLVTDMVLVRMGZNZVTBHHUBOGZVRHBUCOFZBUDZHPOZGZWCHHBUOZWDVLWIWBABUEUFZHHBQH
      UGGQUHUIRWCWEHHPOZWGWCWEHEFGWEWKGABVRUJHWEUKRWCWFHHPWCWIWFHULWJHHBUMRUNUP
      VLHHUQZWBVTWDWHNTWLVLHVGZURHBVRUSUTVAVBVMMVCZVQWATWLWNWMHVDSVPVTCDMVMVOVS
      BVEVFSVHCBVNVKVIVJ $.
      $( [11-Feb-2015] $) $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y a b $.  $d A x y a b $.  $d B x y a b $.  $d F x y a b $.
    c1liplem1.a $e |- ( ph -> A e. RR ) $.
    c1liplem1.b $e |- ( ph -> B e. RR ) $.
    c1liplem1.le $e |- ( ph -> A <_ B ) $.
    c1liplem1.f $e |- ( ph -> F e. ( CC ^pm RR ) ) $.
    c1liplem1.dv $e |- ( ph -> ( ( RR _D F ) |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1liplem1.cn $e |- ( ph -> ( F |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1liplem1.k $e |- K = sup ( ( abs " ( ( RR _D F ) " ( A [,] B ) ) ) ,
        RR , < ) $.
    $( Lemma for ~ c1lip1 . $)
    c1liplem1 $p |- ( ph -> ( K e. RR /\ A. x e. ( A [,] B )
        A. y e. ( A [,] B ) ( x < y -> ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( K x. ( abs ` ( y - x ) ) ) ) ) ) $=
      ( cr wcel cfv cabs cle cc vb va cv clt wbr cmin co cmul wi cicc wral cima
      cdv csup wss c0 wne wrex crn imassrn wf absf frn ax-mp sstri a1i wfun cdm
      dvf ffun cres ccncf ax-resscn cncff sylancr fdm syl ssdmres sylibr lbicc2
      wceq syl3anc wa funfvima2 imp syl21anc fdmi sseqtr4i mp2an ne0i 3syl ssid
      cncfss sseldi cniccbdd fvelima mpan fvres adantl fveq2d rcla4cva eqbrtrrd
      fveq2 breq1d adantll syl5ibcom rexlimdva syl5 breq1 ralrimiv reximdva mpd
      ex suprcl syl5eqel cdiv simplrr ad2antrr ffvelrn syl2anc eqeltrrd simplrl
      cc0 recnd subcl iccssre sseldd resubcl crp simpr difrp mpbid rpne0 absdiv
      wb divcl cioo syl22anc eqtrd sylib syl6eleqr ubicc2 oveq12d oveq1d iccss2
      ltle resabs1 rescncf ctg cnt cpm cnex reex simplbi simprbi ccom copn eqid
      elpm2 tgioo2 dvres iccntr reseq2d dmeqd ioossicc syl5ss sstrd mvth fveq1d
      adantrr ad2antll sseld impr eqeltrd eleq1 expr rexlimdv funfvima syl31anc
      suprub syl6breqr absrpcl elrp ledivmul rpcn mulcom breqtrd ralrimivva jca
      abscl ) AGOPZBUCZCUCZUDUEZUWMFQZUWLFQZUFUGZRQZGUWMUWLUFUGZRQZUHUGZSUEZUIZ
      CDEUJUGZUKBUXDUKAGROFUMUGZUXDULZULZOUDUNZONAUXGOUOZUXGUPUQZUAUCZUBUCZSUEZ
      UAUXGUKZUBOURZUXHOPUXIAUXGRUSZORUXFUTTORVAZUXPOUOVBTORVCVDVEZVFADUXEQZUXF
      PZUXSRQZUXGPZUXJAUXEVGZUXDUXEVHZUOZDUXDPZUXTUYCAUYDTUXEVAZUYCFVIZUYDTUXEV
      JVDZVFAUXEUXDVKZVHUXDWAZUYEAUXDOUYJVAZUYKAOTUOZUYJUXDOVLUGZPUYLVMLUXDOUYJ
      VNVOUXDOUYJVPVQUXDUXEVRVSZADOPZEOPZDESUEUYFHIJDEVTWBUYCUYEWCZUYFUXTUXDDUX
      EWDWEWFRVGZUXFRVHZUOUXTUYBUIUXQUYSVBTORVJVDZUXFTUYTUXFUXEUSZTUXEUXDUTUYGV
      UBTUOUYHUYDTUXEVCVDVETORVBWGZWHUXFUXSRWDWIUXGUYAWJWKZAUWLUYJQZRQZUXLSUEZB
      UXDUKZUBOURZUXOAUYPUYQUYJUXDTVLUGZPVUIHIAUYNVUJUYJUYMTTUOUYNVUJUOVMTWLUXD
      OTWMWILWNUBBDEUYJWOWBAVUHUXNUBOAUXLOPWCZVUHUXNVUKVUHWCZUXMUAUXGUXKUXGPZUW
      MRQZUXKWAZCUXFURZVULUXMUYSVUMVUPVUACUXKUXFRWPWQVULVUOUXMCUXFVULUWMUXFPZWC
      VUNUXLSUEZVUOUXMVULVUQVURVUQUXKUXEQZUWMWAZUAUXDURZVULVURUYCVUQVVAUYIUAUWM
      UXDUXEWPWQVULVUTVURUAUXDVULUXKUXDPZWCVUSRQZUXLSUEZVUTVURVUHVVBVVDVUKVUHVV
      BWCZUXKUYJQZRQZVVCUXLSVVEVVFVUSRVVBVVFVUSWAVUHUXKUXDUXEWRWSWTVUGVVGUXLSUE
      BUXKUXDUWLUXKWAZVUFVVGUXLSVVHVUEVVFRUWLUXKUYJXCWTXDXAXBXEVUTVVCVUNUXLSVUS
      UWMRXCXDXFXGXHWEVUNUXKUXLSXIXFXGXHXJXMXKXLZUBUAUXGXNWBXOZAUXCBCUXDUXDAUWL
      UXDPZUWMUXDPZWCZWCZUWNUXBVVNUWNWCZUWRUWTGUHUGZUXASVVOUWRUWTXPUGZGSUEZUWRV
      VPSUEZVVOUWQUWSXPUGZRQZVVQGSVVOUWQTPZUWSTPZUWSYCUQZVWAVVQWAVVOUWOTPUWPTPV
      WBVVOUWMFUXDVKZQZUWOTVVOVVLVWFUWOWAAVVKVVLUWNXQZUWMUXDFWRVQVVOVWFVVOUXDOV
      WEVAZVVLVWFOPAVWHVVMUWNAUYMVWEUYNPZVWHVMMUXDOVWEVNVOXRZVWGUXDOUWMVWEXSXTY
      DYAVVOUWLVWEQZUWPTVVOVVKVWKUWPWAAVVKVVLUWNYBZUWLUXDFWRVQVVOVWKVVOVWHVVKVW
      KOPVWJVWLUXDOUWLVWEXSXTYDYAUWOUWPYEXTZVVOUWSVVOUWMOPZUWLOPZUWSOPVVOUXDOUW
      MAUXDOUOZVVMUWNAUYPUYQVWPHIDEYFXTXRZVWGYGZVVOUXDOUWLVWQVWLYGZUWMUWLYHXTYD
      ZVVOUWSYIPZVWDVVOUWNVXAVVNUWNYJZVVOVWOVWNUWNVXAYOVWSVWRUWLUWMYKXTYLUWSYMV
      QZUWQUWSYNWBVVOVWAUXHGSVVOUXIUXJUXOVWAUXGPZVWAUXHSUEUXIVVOUXRVFAUXJVVMUWN
      VUDXRAUXOVVMUWNVVIXRVVOUYSVVTUYTPZVVTUXFPZVXDUYSVVOVUAVFVVOVVTTUYTVVOVWBV
      WCVWDVVTTPVWMVWTVXCUWQUWSYPWBVUCUUAVVOUWMFUWLUWMUJUGZVKZQZUWLVXHQZUFUGZUW
      SXPUGZVVTUXFVVOVXKUWQUWSXPVVOVXIUWOVXJUWPUFVVOUWMVXGPZVXIUWOWAVVOVWOVWNUW
      LUWMSUEZVXMVWSVWRVVOUWNVXNVXBVVOVWOVWNUWNVXNUIVWSVWRUWLUWMUUFXTXLZUWLUWMU
      UBWBUWMVXGFWRVQVVOUWLVXGPZVXJUWPWAVVOVWOVWNVXNVXPVWSVWRVXOUWLUWMVTWBUWLVX
      GFWRVQUUCUUDVVOUXLOVXHUMUGZQZVXLWAZUBUWLUWMYQUGZURVXLUXFPZVVOUBUWLUWMVXHV
      WSVWRVXBVVOVWEVXGVKZVXHVXGOVLUGZVVOVXGUXDUOZVYBVXHWAVVOUYPUYQVVKVVLVYDAUY
      PVVMUWNHXRAUYQVVMUWNIXRVWLVWGUYPUYQWCVVMVYDDEUWLUWMUUEWEYRZFVXGUXDUUGVQVV
      OUYMVYDVWIVYBVYCPZUYMVVOVMVFZVYEAVWIVVMUWNMXRUYMVYDWCVWIVYFUXDOVXGVWEUUHW
      EWFYAVVOVXQVHUXEVXTVKZVHZVXTVVOVXQVYHVVOVXQUXEVXGYQUSUUIQZUUJQQZVKZVYHVVO
      UYMFVHZTFVAZVYMOUOZVXGOUOZVXQVYLWAVYGVVOFTOUUKUGPZVYNAVYQVVMUWNKXRZVYQVYN
      VYOTOFUULUUMUUSZUUNVQVVOVYQVYOVYRVYQVYNVYOVYSUUOVQVVOVWOVWNVYPVWSVWRUWLUW
      MYFXTVYMVXGOVYJFRUFUUPUUQQZVYTUURZVYTWUAUUTUVAYRVVOVYKVXTUXEVVOVWOVWNVYKV
      XTWAVWSVWRUWLUWMUVBXTUVCYSZUVDVVOVXTUYDUOVYIVXTWAVVOVXTUXDUYDVVOVXTVXGUXD
      UWLUWMUVEVYEUVFZAUYEVVMUWNUYOXRUVGVXTUXEVRYTYSUVHVVOVXSVYAUBVXTVVNUWNUXLV
      XTPZVXSVYAUIVVNUWNWUDWCZWCZVXRUXFPVXSVYAWUFVXRUXLUXEQZUXFWUFVXRUXLVYHQZWU
      GVVNUWNVXRWUHWAWUDVVOUXLVXQVYHWUBUVIUVJWUDWUHWUGWAVVNUWNUXLVXTUXEWRUVKYSW
      UFUYCUYEUXLUXDPZWUGUXFPZUYCWUFUYIVFAUYEVVMWUEUYOXRVVNUWNWUDWUIVVOVXTUXDUX
      LWUCUVLUVMUYRWUIWUJUXDUXLUXEWDWEWFUVNVXRVXLUXFUVOXFUVPUVQXLYAUYSVXEWCVXFV
      XDUXFVVTRUVRWEWFUBUAUXGVWAUVTUVSNUWAXBVVOUWROPZUWKUWTOPYCUWTUDUEWCZVVRVVS
      YOVVOVWBWUKVWMUWQUWJVQAUWKVVMUWNVVJXRZVVOUWTYIPZWULVVOVWCVWDWUNVWTVXCUWSU
      WBXTZUWTUWCYTUWRGUWTUWDWBYLVVOUWTTPZGTPVVPUXAWAVVOWUNWUPWUOUWTUWEVQVVOGWU
      MYDUWTGUWFXTUWGXMUWHUWI $.
      $( [15-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip1.a $e |- ( ph -> A e. RR ) $.
    c1lip1.b $e |- ( ph -> B e. RR ) $.
    c1lip1.f $e |- ( ph -> F e. ( CC ^pm RR ) ) $.
    c1lip1.dv $e |- ( ph -> ( ( RR _D F ) |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1lip1.cn $e |- ( ph -> ( F |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip1 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( cfv cmin co cabs cle cr cc0 wcel va vb cv cmul wbr cicc wral wrex wa c0
      clt wne 0re ne0i ax-mp ral0 wceq cxr wb rexr icc0 syl2anc biimpar raleqdv
      syl mpbiri ralrimivw r19.2z sylancr wi cdv cima csup adantr simpr cc cres
      cpm ccncf eqid c1liplem1 oveq1 breq2d imbi2d 2ralbidv rcla4ev w3o iccssre
      wss ad2antrr sseld anim12d lttri4 breq1 fveq2 oveq2d fveq2d oveq2 breq12d
      imp imbi12d breq2 oveq1d rcla42v ad2antlr pm2.27 adantl syld 0nn0 nn0ge0i
      a1i fvres ad2antrl wf ax-resscn cncff simpl ffvelrn syl2an eqeltrrd recnd
      subid syl6eq ad3antrrr simprl sseldd simplr mul01 eqtrd 3brtr4d syl5ibcom
      abs0 a1d ancoms ad2antll abssub recn biimpd embantd 3jaodan reximdva mpd
      mpdan ralrimdvva pm2.61ltlei ) ACUCZGMZBUCZGMZNOZPMZFUCZUUFUUHNOZPMZUDOZQ
      UEZCDEUFOZUGZBUUQUGZFRUHZEDAEDUKUEZUIZRUJULZUUSFRUGUUTSRTUVCUMRSUNUOUVBUU
      SFRUVBUUSUURBUJUGUURBUPUVBUURBUUQUJAUUQUJUQZUVAADURTZEURTZUVDUVAUSADRTZUV
      EHDUTVEAERTZUVFIEUTVEDEVAVBVCVDVFVGUUSFRVHVIADEQUEZUIZUAUCZUBUCZUKUEZUVLG
      MZUVKGMZNOZPMZUULUVLUVKNOZPMZUDOZQUEZVJZUBUUQUGUAUUQUGZFRUHZUUTUVJPRGVKOZ
      UUQVLVLRUKVMZRTUVMUVQUWFUVSUDOZQUEZVJZUBUUQUGUAUUQUGZUIUWDUVJUAUBDEGUWFAU
      VGUVIHVNAUVHUVIIVNAUVIVOAGVPRVROTUVIJVNAUWEUUQVQUUQRVSOZTUVIKVNAGUUQVQZUW
      KTZUVILVNUWFVTWAUWCUWJFUWFRUULUWFUQZUWBUWIUAUBUUQUUQUWNUWAUWHUVMUWNUVTUWG
      UVQQUULUWFUVSUDWBWCWDWEWFVEUVJUWCUUSFRUVJUULRTZUIZUWCUUPBCUUQUUQUWPUUHUUQ
      TZUUFUUQTZUIZUIZUUHUUFUKUEZUUHUUFUQZUUFUUHUKUEZWGZUWCUUPVJZUWTUUHRTZUUFRT
      ZUIZUXDUWPUWSUXHUWPUWQUXFUWRUXGUWPUUQRUUHAUUQRWIZUVIUWOAUVGUVHUXIHIDEWHVB
      ZWJZWKUWPUUQRUUFUXKWKWLWTZUUHUUFWMVEUWTUXAUXEUXBUXCUWTUXAUIUWCUXAUUPVJZUU
      PUWSUWCUXMVJUWPUXAUWBUXMUUHUVLUKUEZUVNUUINOZPMZUULUVLUUHNOZPMZUDOZQUEZVJU
      AUBUUHUUFUUQUUQUVKUUHUQZUVMUXNUWAUXTUVKUUHUVLUKWNUYAUVQUXPUVTUXSQUYAUVPUX
      OPUYAUVOUUIUVNNUVKUUHGWOWPWQUYAUVSUXRUULUDUYAUVRUXQPUVKUUHUVLNWRWQWPWSXAU
      VLUUFUQZUXNUXAUXTUUPUVLUUFUUHUKXBUYBUXPUUKUXSUUOQUYBUXOUUJPUYBUVNUUGUUINU
      VLUUFGWOXCWQUYBUXRUUNUULUDUYBUXQUUMPUVLUUFUUHNWBWQWPWSXAXDXEUXAUXMUUPVJUW
      TUXAUUPXFXGXHUWTUXBUIUUPUWCUWTUXBUUPUWTUUIUUINOZPMZUULUUHUUHNOZPMZUDOZQUE
      UXBUUPUWTSSUYDUYGQSSQUEUWTSXIXJXKUWTUYDSPMZSUWTUYCSPUWTUUIVPTZUYCSUQUWTUU
      IUWTUUHUWLMZUUIRUWQUYJUUIUQUWPUWRUUHUUQGXLXMUWPUUQRUWLXNZUWQUYJRTUWSAUYKU
      VIUWOARVPWIUWMUYKXOLUUQRUWLXPVIWJZUWQUWRXQUUQRUUHUWLXRXSXTYAZUUIYBVEWQYLY
      CUWTUYGUULSUDOZSUWTUYFSUULUDUWTUYFUYHSUWTUYESPUWTUUHVPTZUYESUQUWTUUHUWTUU
      QRUUHAUXIUVIUWOUWSUXJYDUWPUWQUWRYEYFYAUUHYBVEWQYLYCWPUWTUULVPTUYNSUQUWTUU
      LUVJUWOUWSYGYAUULYHVEYIYJUXBUYDUUKUYGUUOQUXBUYCUUJPUXBUUIUUGUUINUUHUUFGWO
      XCWQUXBUYFUUNUULUDUXBUYEUUMPUUHUUFUUHNWBWQWPWSYKWTYMUWTUXCUIZUWCUXCUUIUUG
      NOZPMZUULUUHUUFNOZPMZUDOZQUEZVJZUUPUWSUWCVUCVJZUWPUXCUWRUWQVUDUWBVUCUUFUV
      LUKUEZUVNUUGNOZPMZUULUVLUUFNOZPMZUDOZQUEZVJUAUBUUFUUHUUQUUQUVKUUFUQZUVMVU
      EUWAVUKUVKUUFUVLUKWNVULUVQVUGUVTVUJQVULUVPVUFPVULUVOUUGUVNNUVKUUFGWOWPWQV
      ULUVSVUIUULUDVULUVRVUHPUVKUUFUVLNWRWQWPWSXAUVLUUHUQZVUEUXCVUKVUBUVLUUHUUF
      UKXBVUMVUGUYRVUJVUAQVUMVUFUYQPVUMUVNUUIUUGNUVLUUHGWOXCWQVUMVUIUYTUULUDVUM
      VUHUYSPUVLUUHUUFNWBWQWPWSXAXDYNXEUYPUXCVUBUUPUWTUXCVOUYPVUBUUPUYPUYRUUKVU
      AUUOQUWTUYRUUKUQZUXCUWTUYIUUGVPTVUNUYMUWTUUGUWTUUFUWLMZUUGRUWRVUOUUGUQUWP
      UWQUUFUUQGXLYOUWPUYKUWRVUORTUWSUYLUWQUWRVOUUQRUUFUWLXRXSXTYAUUIUUGYPVBVNU
      YPUYTUUNUULUDUWTUYTUUNUQZUXCUWTUXHVUPUXLUXFUYOUUFVPTVUPUXGUUHYQUUFYQUUHUU
      FYPXSVEVNWPWSYRYSXHYTUUCUUDUUAUUBIHUUE $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip2.a $e |- ( ph -> A e. RR ) $.
    c1lip2.b $e |- ( ph -> B e. RR ) $.
    c1lip2.f $e |- ( ph -> F e. ( ( C^n ` RR ) ` 1 ) ) $.
    c1lip2.rn $e |- ( ph -> ran F C_ RR ) $.
    c1lip2.dm $e |- ( ph -> ( A [,] B ) C_ dom F ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip2 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( va c1 cr cfv wcel cc co cc0 ccpn cpm cdvn cdm ccncf wss wa wb ax-resscn
      cn0 1nn0 elcpn mp2an simplbi syl cicc cdv a1i caddc ax-1cn addid2i fveq2i
      cres wceq 0nn0 dvnp1 mp3an13 syl5eqr dvn0 sylancr oveq2d simprbi eqeltrrd
      eqtrd cv wral wi ssid wf cpr reex prid1 nn0uz eleqtri cpnord mp3an sseldi
      cuz crn adantr pmfun fvelrn sylan sseldd ralrimiva cncffvrn syl3anc cncff
      wfun mpd cnex elpm2 dvfre syl2anc eleq2d biimpar ffvelrn rescncf syl21anc
      fdm imp c1lip1 ) ABCDEFGHIAGNOUAPZPZQZGROUBSQZJXOXPNOGUCSZPZGUDZRUESZQZOR
      UFZNUJQXOXPYAUGUHUIUKOGNULUMZUNUOZAYBDEUPSZXSUFZOGUQSZXSOUESZQZYGYEVCYEOU
      ESZQZYBAUIURZLAYGXTQZYIAXRYGXTAXROTXQPZUQSZYGAXRTNUSSZXQPZYOYPNXQNUTVAVBA
      XPYQYOVDZYDYBXPTUJQZYRUIVEOGTVFVGUOVHAYNGOUQAYBXPYNGVDUIYDOGVIVJZVKVNAXOY
      AJXOXPYAYCVLUOVMZARRUFZYBMVOZYGPOQZMXSVPYMYIVQUUBARVRZURZYLAUUDMXSAUUCXSQ
      ZUGZYGUDZOYGVSZUUCUUIQZUUDAUUJUUGAXSOGVSZXSOUFZUUJAYBGYHQZUULUIAGXTQZUUNA
      YNGXTYTAGTXMPZQZYNXTQZAXNUUPGOORVTQYSNTWHPZQXNUUPUFORWAWBVENUJUUSUKWCWDOT
      NWEWFJWGUUQXPUURYBYSUUQXPUURUGUHUIVEOGTULUMVLUOVMAUUBYBUUCGPZOQZMXSVPUUOU
      UNVQUUFYLAUVAMXSUUHGWIZOUUTAUVBOUFUUGKWJAGWSZUUGUUTUVBQAXPUVCYDROGWKUOUUC
      GWLWMWNWOMXSROGWPWQWTZXSOGWRVJAXPUUMYDXPXSRGVSUUMROGXAWAXBVLUOXSGXCXDWJAU
      UKUUGAUUIXSUUCAUUBYMUUIXSVDZUUEUUAUUBYMUGXSRYGVSUVEXSRYGWRXSRYGXJUOVJXEXF
      UUIOUUCYGXGXDWOMXSROYGWPWQWTYBYFUGZYIYKXSOYEYGXHXKXIAYBYFUUNGYEVCYJQZYLLU
      VDUVFUUNUVGXSOYEGXHXKXIXL $.
      $( [11-Feb-2015] $) $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip3.a $e |- ( ph -> A e. RR ) $.
    c1lip3.b $e |- ( ph -> B e. RR ) $.
    c1lip3.f $e |- ( ph -> ( F |` RR ) e. ( ( C^n ` RR ) ` 1 ) ) $.
    c1lip3.rn $e |- ( ph -> ( F " RR ) C_ RR ) $.
    c1lip3.dm $e |- ( ph -> ( A [,] B ) C_ dom F ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip3 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( cr cfv cmin co cabs wral wcel wa cres cmul cle wbr cicc wrex crn df-ima
      cima syl5eqssr cdm cin wss iccssre syl2anc ssin bicomi sylanbrc syl6sseqr
      cv dmres c1lip2 sseld anim12d imp oveqan12rd fveq2d breq1d biimpd anassrs
      wi fvres syl ralimdva reximdv mpd ) ACUTZGMUAZNZBUTZVRNZOPZQNZFUTVQVTOPQN
      UBPZUCUDZCDEUEPZRZBWFRZFMUFVQGNZVTGNZOPZQNZWDUCUDZCWFRZBWFRZFMUFABCDEFVRH
      IJAVRUGGMUIMGMUHKUJAWFMGUKZULZVRUKAWFMUMZWFWPUMZWFWQUMZADMSEMSWRHIDEUNUOZ
      LWRWSTWTWFMWPUPUQURGMVAUSVBAWHWOFMAWGWNBWFAVTWFSZTWEWMCWFAXBVQWFSZWEWMVKZ
      AXBXCTZTVTMSZVQMSZTZXDAXEXHAXBXFXCXGAWFMVTXAVCAWFMVQXAVCVDVEXHWEWMXHWCWLW
      DUCXHWBWKQXGXFVSWIWAWJOVQMGVLVTMGVLVFVGVHVIVMVJVNVNVOVP $.
      $( [16-Nov-2014] $)
  $}

  ${
    $( Membership in a closed real interval.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    elicc4 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
        ( C e. ( A [,] B ) <-> ( A <_ C /\ C <_ B ) ) ) $=
      ( cxr wcel w3a cicc co cle wbr wa wb elicc1 3adant3 3anass baibr 3ad2ant3
      bitr4d ) ADEZBDEZCDEZFCABGHEZUAACIJZCBIJZFZUCUDKZSTUBUELUAABCMNUASUFUELTU
      EUAUFUAUCUDOPQR $.
      $( [16-Nov-2014] $)

    $( Membership in a symmetric closed real interval.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    elicc4abs $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
        ( C e. ( ( A - B ) [,] ( A + B ) ) <-> ( abs ` ( C - A ) ) <_ B ) ) $=
      ( cr wcel w3a cmin co caddc cicc cle wbr wa cabs cfv cxr 3adant3 rexr syl
      wb resubcl readdcl 3ad2ant3 elicc4 syl3anc absdifle 3coml bitr4d ) ADEZBD
      EZCDEZFZCABGHZABIHZJHEZUMCKLCUNKLMZCAGHNOBKLZULUMPEZUNPEZCPEZUOUPTULUMDEZ
      URUIUJVAUKABUAQUMRSULUNDEZUSUIUJVBUKABUBQUNRSUKUIUTUJCRUCUMUNCUDUEUKUIUJU
      QUPTCABUFUGUH $.
      $( [16-Nov-2014] $)
  $}

