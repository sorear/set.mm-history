$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Unsorted preliminaries for Liouville's approximation theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F a $.
    $( Real-coefficient polynomials restrict to real functions.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    plyreres $p |- ( F e. ( Poly ` RR ) -> ( F |` RR ) : RR --> RR ) $=
      ( va cr cply cfv wcel cres wfn crn wss wf cc plybss wb plyf mpbird adantl
      ffn wceq ccj fnssresb 3syl cv wral fvres recn plyrecj sylan2 fveq2d eqtrd
      cjre ffvelrn syl2an cjreb syl eqeltrd ralrimiva fnfvrnss syl2anc sylanbrc
      wa df-f ) ACDEFZACGZCHZVDICJZCCVDKVCVECLJZCAMVCLLAKZALHVEVGNCAOZLLARLCAUA
      UBPZVCVEBUCZVDEZCFZBCUDVFVJVCVMBCVCVKCFZVAZVLVKAEZCVNVLVPSVCVKCAUEQVOVPCF
      ZVPTEZVPSZVOVRVKTEZAEZVPVNVCVKLFZVRWASVKUFZVKAUGUHVOVTVKAVNVTVKSVCVKUKQUI
      UJVOVPLFZVQVSNVCVHWBWDVNVIWCLLVKAULUMVPUNUOPUPUQBCCVDURUSCCVDVBUT $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph i x a b c d $.  $d X i x a b c d $.  $d I i x a b c d $.
    $d A a b c d $.  $d B a b c d $.
    dvmptfsum.x $e |- ( ph -> X C_ RR ) $.
    dvmptfsum.i $e |- ( ph -> I e. Fin ) $.
    dvmptfsum.a $e |- ( ( ph /\ i e. I /\ x e. X ) -> A e. CC ) $.
    dvmptfsum.b $e |- ( ( ph /\ i e. I /\ x e. X ) -> B e. CC ) $.
    dvmptfsum.d $e |- ( ( ph /\ i e. I ) -> ( _D ` ( x e. X |-> A ) ) =
        ( x e. X |-> B ) ) $.
    dvmptfsum.o $e |- ( ph -> X e. ( topGen ` ran (,) ) ) $.
    $( Function-builder for derivative, finite sums rule.  (Contributed by
       Stefan O'Rear, 12-Nov-2014.) $)
    dvmptfsum $p |- ( ph -> ( _D ` ( x e. X |-> sum_ i e. I A ) ) =
        ( x e. X |-> sum_ i e. I B ) ) $=
      ( va vd cmpt cdv wceq wcel wi vb vc wss csu cfv ssid cfn cv csn cun sseq1
      c0 sumeq1 mpteq2dv fveq2d eqeq12d imbi12d imbi2d weq cc0 cc cr a1i wa 0cn
      dvmptc dvmptres sum0 mpteq2i fveq2i 3eqtr4g a1d wn ssun1 sstr mpan imim1i
      wel csb caddc cvv ad2antrr ad3antrrr ad2antlr ssfi syl2anc simp1ll sselda
      co 3expa simplr w3a ax-17 ax17el hbcsb1 hbel eleq1 3anbi3d csbeq1a eleq1d
      chvar syl3anc fsumcl adantlrr sumex hbsum sumeq2sdv cbvmpt eqeq12i biimpi
      hbim ad2antll simplll ssun2 vex snss sylibr simpr wral ancom2s ralrimivva
      3expb rcla42 ancoms mpan9 syl12anc simpll hbmpt csbeq2dv fsumsplit sumsns
      hbcsb mpan2 sylancr oveq2d eqtrd mpteq2dva syl5eq adantrr a2d hbfv anbi2d
      ad2antrl hbeq 3eqtr3g dvmptadd cin simpllr disjsn eqidd exp32 syl5 expcom
      3eqtr4d adantl findcard2s mpcom mpi ) AFFUCZBGFCEUDZPZQUEZBGFDEUDZPZRZFUF
      FUGSZAUUSUVETZIANUHZFUCZBGUVHCEUDZPZQUEZBGUVHDEUDZPZRZTZTAULFUCZBGULCEUDZ
      PZQUEZBGULDEUDZPZRZTZTAUAUHZFUCZBGUWECEUDZPZQUEZBGUWEDEUDZPZRZTZTAUWEUBUH
      ZUIZUJZFUCZBGUWPCEUDZPZQUEZBGUWPDEUDZPZRZTZTAUVGTNUAUBFUVHULRZUVPUWDAUXEU
      VIUVQUVOUWCUVHULFUKUXEUVLUVTUVNUWBUXEUVKUVSQUXEBGUVJUVRUVHULCEUMUNUOUXEBG
      UVMUWAUVHULDEUMUNUPUQURNUAUSZUVPUWMAUXFUVIUWFUVOUWLUVHUWEFUKUXFUVLUWIUVNU
      WKUXFUVKUWHQUXFBGUVJUWGUVHUWECEUMUNUOUXFBGUVMUWJUVHUWEDEUMUNUPUQURUVHUWPR
      ZUVPUXDAUXGUVIUWQUVOUXCUVHUWPFUKUXGUVLUWTUVNUXBUXGUVKUWSQUXGBGUVJUWRUVHUW
      PCEUMUNUOUXGBGUVMUXAUVHUWPDEUMUNUPUQURUVHFRZUVPUVGAUXHUVIUUSUVOUVEUVHFFUK
      UXHUVLUVBUVNUVDUXHUVKUVAQUXHBGUVJUUTUVHFCEUMUNUOUXHBGUVMUVCUVHFDEUMUNUPUQ
      URAUWCUVQABGUTPZQUEUXIUVTUWBABUTUTVAVBGVBVBUCAVBUFVCUTVASZABUHZVBSVDVEVCZ
      UXLABUTUXJAVEVCVFHMVGUVSUXIQBGUVRUTCEVHVIVJBGUWAUTDEVHVIVKVLUWEUGSZUBUAVR
      VMZVDAUWMUXDUXNAUWMUXDTZTUXMAUXNUXOUWMUWQUWLTAUXNVDZUXDUWQUWFUWLUWEUWPUCU
      WQUWFUWEUWOVNUWEUWPFVOVPZVQUXPUWQUWLUXCUXPUWQUWLUXCUXPUWQUWLVDZVDZNGUWEBU
      VHCVSZEUDZEUWNUXTVSZVTWIZPZQUENGUWEBUVHDVSZEUDZEUWNUYEVSZVTWIZPZUWTUXBUXS
      NUYAUYFUYBUYGWAVAGAGVBUCUXNUXRHWBUXPUWQUVHGSZUYAVASUWLUXPUWQVDZUYJVDZUWEU
      XTEUYLUVFUWFUXMAUVFUXNUWQUYJIWCZUWQUWFUXPUYJUXQWDZFUWEWEWFUYLEUAVRZVDAEUH
      ZFSZUYJUXTVASZUYKUYJUYOAAUXNUWQUYJUYOWGWJUYLUWEFUYPUYNWHUYKUYJUYOWKAUYQUX
      KGSZWLZCVASZTAUYQUYJWLZUYRTBNVUBUYRBVUBBWMZBOOUXTVABOUVHCONBWNZWOZOUHZVAS
      ZBWMZWPZXKBNUSZUYTVUBVUAUYRVUJUYSUYJAUYQUXKUVHGWQWRZVUJCUXTVABUVHCWSZWTZU
      QJXAZXBXCXDUYFWASUXSUYJVDUWEUYEEXEVCUWLNGUYAPZQUEZNGUYFPZRZUXPUWQUWLVURUW
      IVUPUWKVUQUWHVUOQBNOGUWGUYAVUFUWGSNWMBOUWEUXTEOUABWNZVUEXFVUJUWECUXTEVULX
      GXHVJBNOGUWJUYFVUFUWJSNWMBOUWEUYEEVUSBOUVHDVUDWOZXFVUJUWEDUYEEBUVHDWSZXGX
      HXIXJXLUXPUWQUYJUYBVASZUWLUYLAUWNFSZUYJVVBAUXNUWQUYJXMZUWQVVCUXPUYJUWQUWO
      FUCZVVCUWOUWPUCUWQVVEUWOUWEXNUWOUWPFVOVPUWNFUBXOZXPXQZWDZUYKUYJXRZAVUAEFX
      SBGXSZVVCUYJVDZVVBAVUABEGFAUYQUYSVUAAUYQUYSVUAJYBXTYAUYJVVCVVJVVBTVUAVVBU
      YRBEUVHUWNGFVUIEOOUYBVAEOUWNUXTOUBEWNZWOVUGEWMZWPVUMEUBUSZUXTUYBVAEUWNUXT
      WSWTYCYDYEYFZXDUXPUWQUYJUYGVASZUWLUYLAVVCUYJVVPVVDVVHVVIADVASZEFXSBGXSZVV
      KVVPAVVQBEGFAUYQUYSVVQAUYQUYSVVQKYBXTYAUYJVVCVVRVVPTVVQVVPUYEVASZBEUVHUWN
      GFBOOUYEVAVUTVUHWPZEOOUYGVAEOUWNUYEVVLWOVVMWPVUJDUYEVAVVAWTZVVNUYEUYGVAEU
      WNUYEWSWTYCYDYEYFZXDUXSAVVCNGUYBPZQUEZNGUYGPZRAUXNUXRYGUWQVVCUXPUWLVVGUUC
      AVVCVDZBGEUWNCVSZPZQUEZBGEUWNDVSZPZVWDVWEAUYQVDZBGCPZQUEZBGDPZRZTVWFVWIVW
      KRZTEUBVWFVWQEVWFEWMEOOVWIVWKEOVWHQVUFQSEWMEBOGVWGVUFGSEWMZEOUWNCVVLWOYHU
      UAEBOGVWJVWREOUWNDVVLWOYHUUDXKVVNVWLVWFVWPVWQVVNUYQVVCAUYPUWNFWQUUBVVNVWN
      VWIVWOVWKVVNVWMVWHQVVNBGCVWGEUWNCWSUNUOVVNBGDVWJEUWNDWSUNUPUQLXAVWHVWCQBN
      OGVWGUYBVUFVWGSNWMBEOUWNUXTOUBBWNZVUEYLVUJUWNWASZVWGUYBRVVFVUJEUWNCUXTWAV
      ULYIYMXHVJBNOGVWJUYGVUFVWJSNWMBEOUWNUYEVWSVUTYLVUJVWTVWJUYGRVVFVUJEUWNDUY
      EWAVVAYIYMXHUUEWFUUFUXSUWSUYDQUXPUWQUWSUYDRUWLUYKUWSNGUWPUXTEUDZPUYDBNOGU
      WRVXAVUFUWRSNWMBOUWPUXTEVUFUWPSBWMZVUEXFVUJUWPCUXTEVULXGXHUYKNGVXAUYCUYLV
      XAUYAUWOUXTEUDZVTWIUYCUYLUWEUWOUXTUWPEUYLUXNUWEUWOUUGULRAUXNUWQUYJUUHUWEU
      WNUUIXQZUYLUWPUUJZUYLUVFUWQUWPUGSUYMUXPUWQUYJWKZFUWPWEWFZUYLUYPUWPSZVDZAU
      YQUYJUYRUYKUYJVXHAAUXNUWQUYJVXHWGWJZUYLUWPFUYPVXFWHZUYKUYJVXHWKZVUNXBYJUY
      LVXCUYBUYAVTUYLVWTVVBVXCUYBRVVFVVOUXTEUWNWAYKYNYOYPYQYRYSUOUXPUWQUXBUYIRU
      WLUYKUXBNGUWPUYEEUDZPUYIBNOGUXAVXMVUFUXASNWMBOUWPUYEEVXBVUTXFVUJUWPDUYEEV
      VAXGXHUYKNGVXMUYHUYLVXMUYFUWOUYEEUDZVTWIUYHUYLUWEUWOUYEUWPEVXDVXEVXGVXIAU
      YQUYJVVSVXJVXKVXLUYTVVQTVUBVVSTBNVUBVVSBVUCVVTXKVUJUYTVUBVVQVVSVUKVWAUQKX
      AXBYJUYLVXNUYGUYFVTUYLVWTVVPVXNUYGRVVFVWBUYEEUWNWAYKYNYOYPYQYRYSUUNUUKYTU
      ULUUMUUOYTUUPUUQUUR $.
      $( [12-Nov-2014] $)
  $}

  ${
    $d x N $.
    $( Derivative of an exponential, possibly zero power.  (Contributed by
       Stefan O'Rear, 13-Nov-2014.) $)
    dvexp2 $p |- ( N e. NN0 -> ( _D ` ( x e. RR |-> ( x ^ N ) ) ) =
        ( x e. RR |-> if ( N = 0 , 0 , ( N x. ( x ^ ( N - 1 ) ) ) ) ) ) $=
      ( wcel cc0 wceq cr cexp co cmpt cdv cfv c1 csn cxp fconstmpt syl mpteq2dv
      wa cc adantl cn0 cv cmin cmul cif ax-1cn dvconst ax-mp recn exp0 mpteq2ia
      eqtr4i fveq2i 3eqtr3i oveq2 fveq2d iftrue wn cn elnn0 orel2 syl5bi impcom
      3eqtr4a wo dvexp iffalse eqtr4d pm2.61dan ) BUACZBDEZAFAUBZBGHZIZJKZAFVKD
      BVLBLUCHGHUDHZUEZIZEVJVKRZAFVLDGHZIZJKZAFDIZVOVRFLMNZJKZFDMNZWBWCLSCWEWFE
      UFLUGUHWDWAJWDAFLIWAAFLOAFVTLVLFCVLSCVTLEVLUIVLUJPUKULUMAFDOUNVSVNWAJVKVN
      WAEVJVKAFVMVTBDVLGUOQTUPVKVRWCEVJVKAFVQDVKDVPUQQTVDVJVKURZRZVOAFVPIZVRWHB
      USCZVOWIEWGVJWJVJWJVKVEWGWJBUTVKWJVAVBVCABVFPWGVRWIEVJWGAFVQVPVKDVPVGQTVH
      VI $.
      $( [13-Nov-2014] $)
  $}

  ${
    $d ph z j k $.  $d A z j k $.  $d B z $.  $d N j k z $.
    dvply1.f $e |- ( ph -> F = ( z e. RR |-> sum_ k e. ( 0 ... N )
        ( ( A ` k ) x. ( z ^ k ) ) ) ) $.
    dvply1.g $e |- ( ph -> G = ( z e. RR |-> sum_ k e. ( 0 ... ( N - 1 ) )
        ( ( B ` k ) x. ( z ^ k ) ) ) ) $.
    dvply1.a $e |- ( ph -> A : NN0 --> CC ) $.
    dvply1.b $e |- B = ( k e. NN0 |-> ( ( k + 1 ) x. ( A ` ( k + 1 ) ) ) ) $.
    dvply1.n $e |- ( ph -> N e. NN0 ) $.
    $( Derivative of a polynomial, explicit sum version.  (Contributed by
       Stefan O'Rear, 13-Nov-2014.) $)
    dvply1 $p |- ( ph -> ( _D ` F ) = G ) $=
      ( cc0 co cmul c1 wcel cc cn0 vj cdv cfv cr cfz cv cexp csu cmpt wceq cmin
      cif fveq2d wss ssid fzfid wa wf elfznn0 ffvelrn syl2an adantr simpr recnd
      a1i ad2antlr expcl syl2anc mulcl 3impa w3a 3adant3 0cnALT wn simpl2 nn0cn
      3syl simpl3 cn wo elnn0 biimpi orel2 sylc nnm1nn0 syl ifclda cvv cz elexi
      0z ovex ifex adantl dvexp2 dvmptcmul cioo crn cmnf cpnf iooretop eqeltrri
      ctg ioomax dvmptfsum wne elfznn nnne0 notnot2 necon1ai iffalse oveq2d cuz
      sumeq2dv 1nn0 nn0uz eleqtri fzss1 sylancr nnnn0 df-ne nn0sscn sseldi cdif
      simplr eqeltrd caddc eldifn ax-1cn addid2i oveq1i eleq2i sylnibr ad2antrr
      eldifi wb sylancl oveq1d oveq1 oveq12d syl6eleq mpbid iftrue mul01 sylan2
      elfzp12 eqtrd fsumss pncan peano2nn0 mulass syl3anc mulcom 3eqtr2d subidi
      sumeq1i weq oveq2 cbvsumv 3eqtr4g 1z nn0z fveq2 fsumshftm 3eqtr4d 3eqtr3d
      id fvmpt2 mpteq2dva eqtr4d 3eqtrd ) AFUBUCBUDNHUEOZEUFZCUCZBUFZUVMUGOZPOZ
      EUHUIZUBUCBUDUVLUVNUVMNUJZNUVMUVOUVMQUKOZUGOZPOZULZPOZEUHZUIZGAFUVRUBIUMA
      BUVQUWDEUVLUDUDUDUNZAUDUOZVEANHUPAUVMUVLRZUVOUDRZUVQSRZAUWIUQZUWJUQZUVNSR
      ZUVPSRZUWKUWLUWNUWJATSCURZUVMTRZUWNUWIKUVMHUSZTSUVMCUTZVAZVBUWMUVOSRZUWQU
      WOUWMUVOUWLUWJVCVDUWIUWQAUWJUWRVFUVOUVMVGVHZUVNUVPVIVHVJAUWIUWJVKZUWNUWCS
      RZUWDSRZAUWIUWNUWJUWTVLUXCUVSNUWBSNSRUXCUVSUQVMVEUXCUVSVNZUQZUVMSRZUWASRZ
      UWBSRZUXGUWIUWQUXHAUWIUWJUXFVOZUWRUVMVPVQUXGUXAUVTTRZUXIUXGUVOAUWIUWJUXFV
      RVDUXGUVMVSRZUXLUXGUXFUXMUVSVTZUXMUXCUXFVCUXGUWIUWQUXNUXKUWRUWQUXNUVMWAWB
      VQUVSUXMWCWDUVMWEZWFUVOUVTVGZVHUVMUWAVIZVHWGUVNUWCVIZVHUWLBUVPUWCUVNWHUDU
      WGUWLUWHVEUXBUWCWHRUWMUVSNUWBNWIWKWJUVMUWAPWLWMVEUWLUWQBUDUVPUIUBUCBUDUWC
      UIUJUWIUWQAUWRWNBUVMWOWFUWTWPUDWQWRXCUCZRAWSWTWQOUDUXSXDWSWTXAXBVEXEAUWFB
      UDNHQUKOZUEOZUVMDUCZUVPPOZEUHZUIGABUDUWEUYDAUWJUQZQHUEOZUWDEUHUYFUVNUWBPO
      ZEUHZUWEUYDUYEUYFUWDUYGEUYEUVMUYFRZUQZUWCUWBUVNPUYJUXFUWCUWBUJZUYIUXFUYEU
      YIUXMUVMNXFZUXFUVMHXGZUVMXHZUXFUVMNUVSXIXJVQWNUVSNUWBXKZWFXLXNUYEUYFUVLUW
      DEUYEQNXMUCZRHTRZUYFUVLUNQTUYPXOXPXQAUYQUWJMVBZQNHTXRXSUYJUWNUXDUXEUYEUWP
      UWQUWNUYIAUWPUWJKVBZUYIUXMUWQUYMUVMXTWFZUWSVAZUYJUWCUWBSUYJUYLUXFUYKUYIUY
      LUYEUYIUXMUYLUYMUYNWFWNUYLUXFUVMNYAWBUYOVQUYJUXHUXIUXJUYJTSUVMYBUYIUWQUYE
      UYTWNYCUYJUXAUXLUXIUYJUVOAUWJUYIYEVDUYIUXLUYEUYIUXMUXLUYMUXOWFWNUXPVHUXQV
      HZYFUXRVHUYEUVMUVLUYFYDRZUQZUWDUVNNPOZNVUDUWCNUVNPVUDUVSUWCNUJVUDUVMNQYGO
      ZHUEOZRZVNZUVSVUHVTZUVSVUCVUIUYEVUCUYIVUHUVMUVLUYFYHVUGUYFUVMVUFQHUEQYIYJ
      YKYLYMWNVUDUWIVUJVUCUWIUYEUVMUVLUYFYOZWNVUDHUYPRZUWIVUJYPAVULUWJVUCAHTUYP
      MXPUUAYNUVMNHUUFWFUUBVUHUVSWCWDUVSNUWBUUCWFXLVUCUYEUWIVUENUJZVUKUYEUWIUQU
      WNVUMUYEUWPUWQUWNUWIUYSUWRUWSVAUVNUUDWFUUEUUGUYENHUPUUHUYEQQUKOZUXTUEOZUA
      UFZQYGOZCUCZVUQUVOVUQQUKOZUGOZPOZPOZUAUHZUYAUVMQYGOZVVDCUCZPOZUVPPOZEUHZU
      YHUYDUYEUYAVVBUAUHUYAVUQVURPOZUVOVUPUGOZPOZUAUHVVCVVHUYEUYAVVBVVKUAUYEVUP
      UYARZUQZVVBVURVUQVVJPOZPOZVURVUQPOZVVJPOZVVKVVMVVAVVNVURPVVMVUTVVJVUQPVVM
      VUSVUPUVOUGVVMVUPSRQSRVUSVUPUJVVMTSVUPYBVVLVUPTRZUYEVUPUXTUSZWNZYCYIVUPQU
      UIYQXLXLXLVVMVURSRZVUQSRZVVJSRZVVQVVOUJVVMUWPVUQTRZVWAAUWPUWJVVLKYNVVLVWD
      UYEVVLVVRVWDVVSVUPUUJWFWNZTSVUQCUTVHZVVMTSVUQYBVWEYCZVVMUXAVVRVWCVVMUVOAU
      WJVVLYEVDVVTUVOVUPVGVHVURVUQVVJUUKUULVVMVVPVVIVVJPVVMVWAVWBVVPVVIUJVWFVWG
      VURVUQUUMVHYRUUNXNVUOUYAVVBUAVUNNUXTUEQYIUUOYKUUPUYAVVGVVKEUAEUAUUQZVVFVV
      IUVPVVJPVWHVVDVUQVVEVURPUVMVUPQYGYSZVWHVVDVUQCVWIUMYTUVMVUPUVOUGUURYTUUSU
      UTUYEUYGVVBEUAQQHQWIRUYEUVAVEZVWJUYEUYQHWIRUYRHUVBWFUYJUWNUXJUYGSRVUAVUBU
      VNUWBVIVHUVMVUQUJZUVNVURUWBVVAPUVMVUQCUVCVWKUVMVUQUWAVUTPVWKUVGVWKUVTVUSU
      VOUGUVMVUQQUKYSXLYTYTUVDUYEUYAUYCVVGEUYEUVMUYARZUQZUYBVVFUVPPVWMUWQVVFWHR
      UYBVVFUJVWLUWQUYEUVMUXTUSWNVVDVVEPWLETVVFWHDLUVHYQYRXNUVEUVFUVIJUVJUVK $.
      $( [13-Nov-2014] $)
  $}

  ${
    $d S a b c p $.  $d F a b c p $.  $d D a b c p $.

    $( An infinite set of values can be extended to a polynomial in at most one
       way.  (Contributed by Stefan O'Rear, 14-Nov-2014.) $)
    plyexmo $p |- ( ( D C_ CC /\ -. D e. Fin ) -> E* p ( p e. ( Poly ` S ) /\
        ( p |` D ) = F ) ) $=
      ( va cc wcel wa cv cfv wceq cmin cc0 c0p wfn cvv wf syl adantr syl2anc vb
      wss cfn wn cply cres wmo weq wi wal cof co csn cxp ccnv cima simplr sseld
      simpll simprll plyf ffn simprrl a1i sselda fnfvof syl22anc simprlr eqtr4d
      cnex simprrr fveq1d fvres adantl 3eqtr3d ffvelrn subeq0 mpbird eqtrd jcad
      wb ex plysubcl 0cn elexi fniniseg sylancl sylibrd ssrdv ssfi expcom chash
      mtod cdgr cle wbr df-ne biimpri eqid fta1 syl2an simpld mt3d df-0p syl6eq
      ofsubeq0 syl3anc mpbid alrimivv eleq1 reseq1 eqeq1d anbi12d sylibr plyssc
      wne mo4 sseli anim1i immoi ) AFUBZAUCGZUDZHZDIZFUEJZGZYEAUFZCKZHZDUGZYEBU
      EJZGZYIHZDUGYDYJEIZYFGZYOAUFZCKZHZHZDEUHZUIZEUJDUJYKYDUUBDEYDYTUUAYDYTHZY
      EYOLUKULZFMUMZUNZKZUUAUUCUUDNUUFUUCUUDNKZUUDUOUUEUPZUCGZUUCUUJYBYAYCYTUQU
      UCAUUIUBZUUJYBUIUUCUAAUUIUUCUAIZAGZUULFGZUULUUDJZMKZHZUULUUIGZUUCUUMUUNUU
      PUUCAFUULYAYCYTUSZURUUCUUMUUPUUCUUMHZUUOUULYEJZUULYOJZLULZMUUTYEFOZYOFOZF
      PGZUUNUUOUVCKUUCUVDUUMUUCFFYEQZUVDUUCYGUVGYDYGYIYSUTZFYEVARZFFYEVBRSUUCUV
      EUUMUUCFFYOQZUVEUUCYPUVJYDYJYPYRVCZFYOVARZFFYOVBRSUVFUUTVJVDUUCAFUULUUSVE
      ZFLYEYOPUULVFVGUUTUVCMKZUVAUVBKZUUTUULYHJZUULYQJZUVAUVBUUTUULYHYQUUCYHYQK
      UUMUUCYHCYQYDYGYIYSVHYDYJYPYRVKVISVLUUMUVPUVAKUUCUULAYEVMVNUUMUVQUVBKUUCU
      ULAYOVMVNVOUUTUVAFGZUVBFGZUVNUVOWAUUTUVGUUNUVRUUCUVGUUMUVISUVMFFUULYEVPTU
      UTUVJUUNUVSUUCUVJUUMUVLSUVMFFUULYOVPTUVAUVBVQTVRVSWBVTUUCUUDFOZMPGUURUUQW
      AUUCFFUUDQZUVTUUCUUDYFGZUWAUUCYGYPUWBUVHUVKFYEYOWCTZFUUDVARFFUUDVBRMFWDWE
      FMUULUUDPWFWGWHWIUUJUUKYBUUIAWJWKRWMUUCUUHUDZUUJUUCUWDHUUJUUIWLJUUDWNJWOW
      PZUUCUWBUUDNXPZUUJUWEHUWDUWCUWFUWDUUDNWQWRUUIFUUDUUIWSWTXAXBWBXCXDXEUUCUV
      FUVGUVJUUGUUAWAUVFUUCVJVDUVIUVLFYEYOPXFXGXHWBXIYJYSDEUUAYGYPYIYRYEYOYFXJU
      UAYHYQCYEYOAXKXLXMXQXNYNYJDYMYGYIYLYFYEBXOXRXSXTR $.
      $( [14-Nov-2014] $)
  $}

  ${
    $d F a b c d e g $.
    $( The derivative of a polynomial is a polynomial up to domain
       difficulties.  (Contributed by Stefan O'Rear, 14-Nov-2014.) $)
    dvply2 $p |- ( F e. ( Poly ` S ) -> E! g e. ( Poly ` CC )
        ( _D ` ( F |` RR ) ) = ( g |` RR ) ) $=
      ( va vb vc cfv wcel cc cr wceq wa cc0 c1 co cn0 cmul cmpt cn c0 cply cres
      cdv wreu plyssc sseli wrex wmo cdgr cmin cfz caddc ccoe cexp csu wss ssid
      cv a1i nnm1nn0 adantl peano2nn0 nn0cn syl eqid coef3 ffvelrn syl2an mulcl
      wf syl2anc fmptd adantr elplyr syl3anc wn dgrcl elnn0 sylib orcanai oveq1
      c0p wo oveq2d clt wbr cneg df-neg lt01 wb 1re lt0neg2 ax-mp mpbi eqbrtrri
      cz 0z 1z zsubcl mp2an fzn syl6eq sumeq1d sum0 mpteq2dva csn cxp fconstmpt
      df-0p eqtri syl6eqr syl6eqel pm2.61dan coeid reseq1d ax-resscn resmpt weq
      fveq2d oveq12d cbvmptv dvply1 reseq1 eqeq2d rcla4ev cfn com ominf cvv cen
      ply0 omex nnenom enfi mtbir nnssre ssfi mpan2 mto plyexmo anbi2i sylanbrc
      eqcom mobii reu5 ) CAUAGZHCIUAGZHZCJUBZUCGZBURZJUBZKZBUUGUDZUUFUUGCAUEUFU
      UHUUMBUUGUGZUUKUUGHZUUMLZBUHZUUNUUHDIMCUIGZNUJOZUKOZEURZFPFURZNULOZUVDCUM
      GZGZQOZRZGDURZUVBUNOZQOZEUOZRZUUGHZUUJUVMJUBZKZUUOUUHUUSSHZUVNUUHUVQLZIIU
      PZUUTPHZPIUVHVJZUVNUVSUVRIUQZUSUVQUVTUUHUUSUTVAUUHUWAUVQUUHFPUVGIUVHUUHUV
      CPHZLUVDIHZUVFIHZUVGIHUWCUWDUUHUWCUVDPHZUWDUVCVBZUVDVCVDVAUUHPIUVEVJUWFUW
      EUWCUVEICUVEVEZVFZUWGPIUVDUVEVGVHUVDUVFVIVKUVHVEVLVMDUVHIEUUTVNVOUUHUVQVP
      LZUVMWBUUGUWJUVMDIMRZWBUWJDIUVLMUWJUVIIHZLZUVLTUVKEUOMUWMUVATUVKEUWJUVATK
      ZUWLUWJUUSMKZUWNUUHUVQUWOUUHUUSPHUVQUWOWCICVQZUUSVRVSVTUWOUVAMMNUJOZUKOZT
      UWOUUTUWQMUKUUSMNUJWAWDUWQMWEWFZUWRTKZNWGZUWQMWENWHMNWEWFZUXAMWEWFZWINJHU
      XBUXCWJWKNWLWMWNWOMWPHZUWQWPHZUWSUWTWJWQUXDNWPHUXEWQWRMNWSWTMUWQXAWTWNXBV
      DVMXCUVKEXDXBXEWBIMXFXGUWKXIDIMXHXJXKUVSWBUUGHUWBIYKWMXLXMUUHDUVEUVHEUUIU
      VOUUSUUHUUIDIMUUSUKOUVBUVEGUVJQOEUOZRZJUBZDJUXFRZUUHCUXGJDUVEIECUUSUWHUUS
      VEXNXOJIUPZUXHUXIKXPDIJUXFXQWMXBUVODJUVLRKZUUHUXJUXKXPDIJUVLXQWMUSUWIFEPU
      VGUVBNULOZUXLUVEGZQOFEXRZUVDUXLUVFUXMQUVCUVBNULWAZUXNUVDUXLUVEUXOXSXTYAUW
      PYBUUMUVPBUVMUUGUUKUVMKUULUVOUUJUUKUVMJYCYDYEVKUURUUHUUPUULUUJKZLZBUHZUUR
      UXJJYFHZVPUXRXPUXSSYFHZUXTYGYFHZYHYGYIHSYGYJWFUXTUYAWJYLYMSYGYIYNWTYOUXSS
      JUPUXTYPJSYQYRYSJIUUJBYTWTUXQUUQBUXPUUMUUPUULUUJUUCUUAUUDWNUSUUMBUUGUUEUU
      BVD $.
      $( [14-Nov-2014] $)
  $}

  ${
    $d F a b c d $.
    $( Strengthening of ~ dvf .  (Contributed by Stefan O'Rear,
       15-Nov-2014.) $)
    dvf2 $p |- ( _D ` F ) : dom ( _D ` F ) --> CC $=
      ( va vd vb vc cdv cdm wcel cfv cc wf cr cpm co wss wa cv cioo cmin c0 crn
      ctg cnt weq cdiv cif cmpt csubsp cabs ccom copn copab df-dv dmmptss sseli
      ccnp cnex reex elpm2 biimpi dvf 3syl wn wceq ndmfv dmeq dm0 syl6eq feq12d
      f0 id mpbiri syl pm2.61i ) AFGZHZAFIZGZJVQKZVPAJLMNZHZAGZJAKWBLOPZVSVOVTA
      BVTCQZBQZGZRUAUBIZUCIIHDWFDCUDEQDQZWEIWDWEISNWHWDSNUENUFUGWDWFWGUHNUISUJU
      KIUPNIHPCEULFCEDBUMUNUOWAWCJLAUQURUSUTWBAVAVBVPVCVQTVDZVSAFVEWIVSTJTKJVJW
      IVRTJVQTWIVKWIVRTGTVQTVFVGVHVIVLVMVN $.
      $( [15-Nov-2014] $)
  $}

  ${
    $d F a b c $.  $d N a b c $.  $d M a b c $.
    $( Zero times iterated derivative.  (Contributed by Stefan O'Rear,
       15-Nov-2014.) $)
    dvn0 $p |- ( F e. ( CC ^pm RR ) -> ( 0 Dn F ) = F ) $=
      ( va vb cc cr cpm co wcel cc0 cdvn cdv c1st cn0 csn cxp cseq wceq 0nn0 cv
      cfv ccom fveq2 sneq xpeq2d seqeq3d fveq1d df-dvn fvex ovmpt2 0z fvconst2g
      mpan mpan2 seq1i eqtrd ) ADEFGZHZIAJGZIKLUAZMANZOZIPZTZAIMHZUQURVCQRBCIAM
      UPBSZUSMCSZNZOZIPZTVCJIVITVEIVIUBVFAQZIVIVBVJVHVAUSIVJVGUTMVFAUCUDUEUFBCU
      GIVBUHUIULUQAUSVAIUJUQVDIVATAQRMAIUPUKUMUNUO $.
      $( [15-Nov-2014] $)

    $( Successor iterated derivative.  (Contributed by Stefan O'Rear,
       15-Nov-2014.) $)
    dvnp1 $p |- ( ( F e. ( CC ^pm RR ) /\ N e. NN0 ) ->
        ( ( N + 1 ) Dn F ) = ( _D ` ( N Dn F ) ) ) $=
      ( va vb co wcel cn0 cdv c1st csn cxp cc0 cseq cfv cdvn wceq adantl cvv cv
      fvex cc cr cpm wa c1 caddc cuz elnn0uz seqp1 sylbi cop df-ov wf wfo fo1st
      ccom fof ax-mp opex fvco3 mp2an op1st fveq2i 3eqtri peano2nn0 simpl fveq2
      syl6eq xpeq2d seqeq3d fveq1d df-dvn ovmpt2 syl2anc ancoms fveq2d 3eqtr4d
      sneq ) AUAUBUCEZFZBGFZUDZBUEUFEZHIUPZGAJZKZLMZNZBWGNZHNZWCAOEZBAOEZHNWBWH
      WIWCWFNZWDEZWJWAWHWNPZVTWABLUGNFWOBUHWDWFLBUIUJQWNWIWMUKZWDNZWPINZHNZWJWI
      WMWDULRRIUMZWPRFWQWSPRRIUNWTUORRIUQURWIWMUSRRWPHIUTVAWRWIHWIWMBWGTZVBVCVD
      VHWBWCGFZVTWKWHPWAXBVTBVEQVTWAVFCDWCAGVSCSZWDGDSZJZKZLMZNZWHOWCXGNXCWCXGV
      GXDAPZWCXGWGXIXFWFWDLXIXEWEGXDAVRVIVJZVKCDVLZWCWGTVMVNWBWLWIHWAVTWLWIPCDB
      AGVSXHWIOBXGNXCBXGVGXIBXGWGXJVKXKXAVMVOVPVQ $.
      $( [15-Nov-2014] $)

    $( The N-times derivative is a function.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    dvnf $p |- ( ( F e. ( CC ^pm RR ) /\ N e. NN0 ) ->
        ( N Dn F ) : dom ( N Dn F ) --> CC ) $=
      ( cn0 wcel cc cr cpm co cn cc0 wceq wo cdvn cdm wf elnn0 wa c1 cmin syl
      cdv cfv caddc nncn ax-1cn npcan sylancl adantl oveq1d dvnp1 sylan2 eqtr3d
      nnm1nn0 dvf2 id dmeq feq12d mpbiri oveq1 dvn0 sylan9eqr simpl eqeltrd wss
      cnex reex elpm2 simplbi jaodan sylan2b ) BCDAEFGHZDZBIDZBJKZLBAMHZNZEVOOZ
      BPVLVMVQVNVLVMQZVOBRSHZAMHZUAUBZKZVQVRVSRUCHZAMHZVOWAVRWCBAMVMWCBKZVLVMBE
      DREDWEBUDUEBRUFUGUHUIVMVLVSCDWDWAKBUMAVSUJUKULWBVQWANZEWAOVTUNWBVPWFEVOWA
      WBUOVOWAUPUQURTVLVNQZVOVKDZVQWGVOAVKVNVLVOJAMHABJAMUSAUTVAVLVNVBVCWHVQVPF
      VDEFVOVEVFVGVHTVIVJ $.
      $( [16-Nov-2014] $)

    $( The set of N-times differentiable points is a subset of the domain of
       the function.  (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    dvnbss $p |- ( ( F e. ( CC ^pm RR ) /\ N e. NN0 ) ->
        dom ( N Dn F ) C_ dom F ) $=
      ( va vb cn0 wcel cc cr co cdvn cdm wss cc0 wceq oveq1 dmeqd sseq1d imbi2d
      wi syl2anc cpm cv caddc weq dvn0 ssid a1i eqsstrd w3a cdv cfv simp2 simp1
      c1 wa dvnp1 wf dvnf simp3 cnex reex elpm2 simprbi 3ad2ant2 sstrd 3exp a2d
      dvbss nn0ind impcom ) BEFAGHUAIFZBAJIZKZAKZLZVKCUBZAJIZKZVNLZSVKMAJIZKZVN
      LZSVKDUBZAJIZKZVNLZSVKWCUNUCIZAJIZKZVNLZSVKVOSCDBVPMNZVSWBVKWKVRWAVNWKVQV
      TVPMAJOPQRCDUDZVSWFVKWLVRWEVNWLVQWDVPWCAJOPQRVPWGNZVSWJVKWMVRWIVNWMVQWHVP
      WGAJOPQRVPBNZVSVOVKWNVRVMVNWNVQVLVPBAJOPQRVKWAVNVNVKVTAAUEPVNVNLVKVNUFUGU
      HWCEFZVKWFWJWOVKWFWJWOVKWFUIZWIWEVNWPWIWDUJUKZKZWEWPVKWOWIWRNWOVKWFULZWOV
      KWFUMZVKWOUOWHWQAWCUPPTWPWEGWDUQZWEHLWRWELWPVKWOXAWSWTAWCURTWPWEVNHWOVKWF
      USZVKWOVNHLZWFVKVNGAUQXCGHAUTVAVBVCVDVEWEWDVHTUHXBVEVFVGVIVJ $.
      $( [16-Nov-2014] $)

    $( The ` C^n ` object is a function.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    fncpn $p |- C^n Fn NN0 $=
      ( va vb cv cdvn co cdm cc ccncf wcel cr cpm crab cvv cn0 wfn df-cpn fnmpt
      ccpn ovex rabex a1i mprg ) ACZBCZDEUDFGHEIZBGJKEZLZMIZRNOANANUGRMABPQUHUC
      NIUEBUFGJKSTUAUB $.
      $( [16-Nov-2014] $)

    $( Condition for n-times continuous differentiability.  (Contributed by
       Stefan O'Rear, 15-Nov-2014.) $)
    elcpn $p |- ( N e. NN0 -> ( F e. ( C^n ` N ) <-> ( F e. ( CC ^pm RR ) /\
          ( N Dn F ) e. ( dom F -cn-> CC ) ) ) ) $=
      ( vb va cn0 wcel ccpn cfv cv cdvn co cdm cc ccncf cr cpm crab wceq oveq1
      wa eleq1d rabbidv df-cpn ovex rabex fvmpt eleq2d oveq2 dmeq eleq12d elrab
      oveq1d syl6bb ) BEFZABGHZFABCIZJKZUPLZMNKZFZCMOPKZQZFAVAFBAJKZALZMNKZFZTU
      NUOVBADBDIZUPJKZUSFZCVAQVBEGVGBRZVIUTCVAVJVHUQUSVGBUPJSUAUBDCUCUTCVAMOPUD
      UEUFUGUTVFCAVAUPARZUQVCUSVEUPABJUHVKURVDMNUPAUIULUJUKUM $.
      $( [15-Nov-2014] $)

    $( ` C^n ` conditions are ordered by strength.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    cpnord $p |- ( ( M e. NN0 /\ N e. NN0 /\ M <_ N ) ->
        ( C^n ` N ) C_ ( C^n ` M ) ) $=
      ( va cn0 wcel cle wbr ccpn cfv wss wi co wceq fveq2 sseq1d imbi2d cc0 cdm
      cr cc vb w3a simp1 cz nn0z id cv c1 caddc weq ssid a1ii wa simpl2 0re a1i
      nn0re adantl zre syl nn0ge0 simpl3 letrd elnn0z sylanbrc cpm ccncf wf cdv
      cdvn simp2 dvnf syl2anc dvnbss dvnp1 dmeqd cncff mpan fdm 3ad2ant3 eqtr3d
      cnex reex elpm2 simprbi 3ad2ant2 sstrd dvbss eqsstr3d eqssd feq2d syl3anc
      mpbid dvcn 3exp imdistand wb peano2nn0 elcpn 3imtr4d ssrdv sstr2 3syl a2d
      ex uzind syl3an mpd ) ADEZBDEZABFGZUBXIBHIZAHIZJZXIXJXKUCXIAUDEZXJBUDEXKX
      KXIXNKZAUEBUEXKUFXICUGZHIZXMJZKXIXMXMJZKXIUAUGZHIZXMJZKXIYAUHUILZHIZXMJZK
      XPCUAABXQAMZXSXTXIYGXRXMXMXQAHNOPCUAUJZXSYCXIYHXRYBXMXQYAHNOPXQYDMZXSYFXI
      YIXRYEXMXQYDHNOPXQBMZXSXNXIYJXRXLXMXQBHNOPXOXIXTXMUKULXOYAUDEZAYAFGZUBZXI
      YCYFYMXIYCYFKZYMXIUMZYADEZYEYBJYNYOYKQYAFGYPXOYKYLXIUNZYOQAYAQSEYOUOUPXIA
      SEYMAUQURYOYKYASEYQYAUSUTXIQAFGYMAVAURXOYKYLXIVBVCYAVDVEYPCYEYBYPXQTSVFLE
      ZYDXQVJLZXQRZTVGLZEZUMZYRYAXQVJLZUUAEZUMXQYEEZXQYBEYPYRUUBUUEYPYRUUBUUEYP
      YRUUBUBZYTTUUDVHZYTSJZUUDVIIZRZYTMUUEUUGUUDRZTUUDVHZUUHUUGYRYPUUMYPYRUUBV
      KZYPYRUUBUCZXQYAVLVMZUUGUULYTTUUDUUGUULYTUUGYRYPUULYTJUUNUUOXQYAVNVMZUUGY
      TUUKUULUUGYSRZUUKYTUUGYSUUJUUGYRYPYSUUJMUUNUUOXQYAVOVMVPUUBYPUURYTMZYRUUB
      YTTYSVHZUUSTTJUUBUUTTUKYTTYSVQVRYTTYSVSUTVTWAZUUGUUMUULSJUUKUULJUUPUUGUUL
      YTSUUQYRYPUUIUUBYRYTTXQVHUUITSXQWBWCWDWEWFZWGUULUUDWHVMWIWJWKWMUVBUVAYTUU
      DWNWLWOWPYPYDDEUUFUUCWQYAWRXQYDWSUTXQYAWSWTXAYEYBXMXBXCXEXDXFXGXH $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d F a b c d g $.  $d N a b c d g $.

    $( Polynomials have polynomials as derivatives of all orders.  (Contributed
       by Stefan O'Rear, 15-Nov-2014.) $)
    dvnply $p |- ( ( F e. ( Poly ` S ) /\ N e. NN0 ) -> E! g e. ( Poly ` CC )
        ( g |` RR ) = ( N Dn ( F |` RR ) ) ) $=
      ( va cfv wcel wa cr cdvn co wceq cc wrex wi oveq1 eqeq2d rexbidv cvv cn
      vb cply cn0 cv cres wmo wreu plyssc sseli cc0 c1 caddc imbi2d weq cpm wss
      wf plyf ax-resscn fssres sylancl ssid cnex reex elpm2r mpanl12 syl eqcomd
      dvn0 reseq1 eqeq1d rcla4ev mpdan cdv dvply2 reurex simplrr fveq2d simpllr
      ad2antrl simplll dvnp1 eqtr4d eqeq1 syl5ibcom reximdva mpd exp32 rexlimdv
      syl2anc cbvrexv syl6ib ex a2d nn0ind mpan9 cfn wn com ominf cen wb nnenom
      wbr omex enfi mp2an mtbir nnssre ssfi mpan2 mto plyexmo a1i reu5 sylanbrc
      ) CAUBFZGZDUCGZHZBUDZIUEZDCIUEZJKZLZBMUBFZNZYAYFGZYEHBUFZYEBYFUGXRCYFGZXS
      YGXQYFCAUHUIYJYBEUDZYCJKZLZBYFNZOYJYBUJYCJKZLZBYFNZOYJYBUAUDZYCJKZLZBYFNZ
      OYJYBYRUKULKZYCJKZLZBYFNZOYJYGOEUADYKUJLZYNYQYJUUFYMYPBYFUUFYLYOYBYKUJYCJ
      PQRUMEUAUNZYNUUAYJUUGYMYTBYFUUGYLYSYBYKYRYCJPQRUMYKUUBLZYNUUEYJUUHYMUUDBY
      FUUHYLUUCYBYKUUBYCJPQRUMYKDLZYNYGYJUUIYMYEBYFUUIYLYDYBYKDYCJPQRUMYJYCYOLZ
      YQYJYOYCYJYCMIUOKGZYOYCLYJIMYCUQZIIUPZUUKYJMMCUQIMUPZUULMCURUSMMICUTVAIVB
      MSGISGUULUUMHUUKVCVDMIIYCSSVEVFVAZYCVIVGVHYPUUJBCYFYACLYBYCYOYACIVJVKVLVM
      YRUCGZYJUUAUUEUUPYJUUAUUEOUUPYJHZUUAYKIUEZUUCLZEYFNZUUEUUQYTUUTBYFUUQYHYT
      UUTUUQYHYTHZHZYBVNFZUURLZEYFNZUUTYHUVEUUQYTYHUVDEYFUGUVEMEYAVOUVDEYFVPVGV
      TUVBUVDUUSEYFUVBYKYFGZHZUVCUUCLUVDUUSUVGUVCYSVNFZUUCUVGYBYSVNUUQYHYTUVFVQ
      VRUVGUUKUUPUUCUVHLUVGYJUUKUUPYJUVAUVFVSUUOVGUUPYJUVAUVFWAYCYRWBWJWCUVCUUR
      UUCWDWEWFWGWHWIUUSUUDEBYFEBUNUURYBUUCYKYAIVJVKWKWLWMWNWOWPYIXTUUNIWQGZWRY
      IUSUVITWQGZUVJWSWQGZWTWSSGTWSXAXDUVJUVKXBXEXCTWSSXFXGXHUVITIUPUVJXIITXJXK
      XLIMYDBXMXGXNYEBYFXOXP $.
      $( [15-Nov-2014] $)

    $( Polynomials are smooth.  (Contributed by Stefan O'Rear, 16-Nov-2014.) $)
    plycpn $p |- ( F e. ( Poly ` S ) -> ( F |` RR ) e. |^| ran C^n ) $=
      ( va vb cply cfv wcel cc cr cres ccpn wceq cn0 wb wa co wss ax-resscn syl
      ccncf crn cint plyssc sseli cv wral wrex wfn fncpn fvelrnb ax-mp cpm cdvn
      cdm adantl wf plyf fssres sylancl reex cnex fpm adantr wreu dvnply reurex
      elcpn plycn wi ssid rescncf mp2an syl5sseqr ssdmres sylib ad2antrr oveq1d
      eleqtrrd eleq1 syl5ibcom rexlimdva mpbir2and eleq2 syl5bi ralrimiv resexg
      fdm mpd cvv elintg mpbird ) BAEFZGBHEFZGZBIJZKUAZUBGZWLWMBAUCUDWNWQWOCUEZ
      GZCWPUFZWNWSCWPWRWPGZDUEZKFZWRLZDMUGZWNWSKMUHXAXENUIDMWRKUJUKWNXDWSDMWNXB
      MGZOZWOXCGZXDWSXGXHWOHIULPGZXBWOUMPZWOUNZHTPZGZXFXHXIXMONWNWOXBVGUOWNXIXF
      WNIHWOUPZXIWNHHBUPZIHQZXNHBUQZRHHIBURUSIHWOUTVAVBSVCXGWRIJZXJLZCWMUGZXMXG
      XSCWMVDXTHCBXBVEXSCWMVFSXGXSXMCWMXGWRWMGZOZXRXLGXSXMYBXRIHTPZXLYAXRYCGZXG
      YAWRHHTPGZYDHWRVHHHQXPYEYDVIHVJRHHIWRVKVLSUOYBXKIHTWNXKILZXFYAWNIBUNZQYFW
      NHIYGRWNXOYGHLXQHHBWGSVMIBVNVOVPVQVRXRXJXLVSVTWAWHWBXCWRWOWCVTWAWDWEWNWOW
      IGWQWTNBIWMWFCWOWPWIWJSWKS $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y a b $.  $d A x y a b $.  $d B x y a b $.  $d F x y a b $.
    c1liplem1.a $e |- ( ph -> A e. RR ) $.
    c1liplem1.b $e |- ( ph -> B e. RR ) $.
    c1liplem1.le $e |- ( ph -> A <_ B ) $.
    c1liplem1.f $e |- ( ph -> F e. ( CC ^pm RR ) ) $.
    c1liplem1.dv $e |- ( ph -> ( ( _D ` F ) |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1liplem1.cn $e |- ( ph -> ( F |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1liplem1.k $e |- K = sup ( ( abs " ( ( _D ` F ) " ( A [,] B ) ) ) ,
        RR , < ) $.
    $( Lemma for ~ c1lip1 . $)
    c1liplem1 $p |- ( ph -> ( K e. RR /\ A. x e. ( A [,] B )
        A. y e. ( A [,] B ) ( x < y -> ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( K x. ( abs ` ( y - x ) ) ) ) ) ) $=
      ( cr wcel cfv cabs cle cc vb va cv clt wbr cmin co cmul wi cicc wral cima
      cdv csup wss c0 wne wrex crn imassrn wf absf frn ax-mp sstri a1i wfun cdm
      dvf2 ffun cres wceq ccncf ax-resscn sylancr fdm syl ssdmres sylibr lbicc2
      cncff syl3anc funfvima2 imp syl21anc fdmi sseqtr4i mp2an ne0i 3syl cncfss
      wa ssid sseldi cniccbdd fvelima mpan fvres adantl fveq2d weq fveq2 breq1d
      rcla4cva eqbrtrrd adantll syl5ibcom rexlimdva breq1 ralrimiv reximdva mpd
      syl5 suprcl syl5eqel cdiv simplrr ad2antrr ffvelrn syl2anc recnd eqeltrrd
      ex simplrl subcl iccssre sseldd resubcl simpr wb difrp mpbid rpne0 absdiv
      cc0 crp divcl cioo eqtrd sylib ltle ubicc2 oveq12d oveq1d iccss2 syl22anc
      syl6eleqr resabs1 rescncf ctg cnt cpm cnex reex elpm2 simplbi eqid iccntr
      simprbi dvres reseq2d dmeqd ioossicc syl5ss sstrd fveq1d adantrr ad2antll
      mvth sseld impr eqeltrd eleq1 rexlimdv funfvima suprub syl31anc syl6breqr
      expr abscl absrpcl elrp ledivmul rpcn mulcom breqtrd ralrimivva jca ) AGO
      PZBUCZCUCZUDUEZUWKFQZUWJFQZUFUGZRQZGUWKUWJUFUGZRQZUHUGZSUEZUIZCDEUJUGZUKB
      UXBUKAGRFUMQZUXBULZULZOUDUNZONAUXEOUOZUXEUPUQZUAUCZUBUCZSUEZUAUXEUKZUBOUR
      ZUXFOPUXGAUXERUSZORUXDUTTORVAZUXNOUOVBTORVCVDVEZVFADUXCQZUXDPZUXQRQZUXEPZ
      UXHAUXCVGZUXBUXCVHZUOZDUXBPZUXRUYAAUYBTUXCVAZUYAFVIZUYBTUXCVJVDZVFAUXCUXB
      VKZVHUXBVLZUYCAUXBOUYHVAZUYIAOTUOZUYHUXBOVMUGZPUYJVNLUXBOUYHWAVOUXBOUYHVP
      VQUXBUXCVRVSZADOPZEOPZDESUEUYDHIJDEVTWBUYAUYCWLZUYDUXRUXBDUXCWCWDWERVGZUX
      DRVHZUOUXRUXTUIUXOUYQVBTORVJVDZUXDTUYRUXDUXCUSZTUXCUXBUTUYEUYTTUOUYFUYBTU
      XCVCVDVETORVBWFZWGUXDUXQRWCWHUXEUXSWIWJZAUWJUYHQZRQZUXJSUEZBUXBUKZUBOURZU
      XMAUYNUYOUYHUXBTVMUGZPVUGHIAUYLVUHUYHUYKTTUOUYLVUHUOVNTWMUXBOTWKWHLWNUBBD
      EUYHWOWBAVUFUXLUBOAUXJOPWLZVUFUXLVUIVUFWLZUXKUAUXEUXIUXEPZUWKRQZUXIVLZCUX
      DURZVUJUXKUYQVUKVUNUYSCUXIUXDRWPWQVUJVUMUXKCUXDVUJUWKUXDPZWLVULUXJSUEZVUM
      UXKVUJVUOVUPVUOUXIUXCQZUWKVLZUAUXBURZVUJVUPUYAVUOVUSUYGUAUWKUXBUXCWPWQVUJ
      VURVUPUAUXBVUJUXIUXBPZWLVUQRQZUXJSUEZVURVUPVUFVUTVVBVUIVUFVUTWLZUXIUYHQZR
      QZVVAUXJSVVCVVDVUQRVUTVVDVUQVLVUFUXIUXBUXCWRWSWTVUEVVEUXJSUEBUXIUXBBUAXAZ
      VUDVVEUXJSVVFVUCVVDRUWJUXIUYHXBWTXCXDXEXFVURVVAVULUXJSVUQUWKRXBXCXGXHXMWD
      VULUXIUXJSXIXGXHXMXJYCXKXLZUBUAUXEXNWBXOZAUXABCUXBUXBAUWJUXBPZUWKUXBPZWLZ
      WLZUWLUWTVVLUWLWLZUWPUWRGUHUGZUWSSVVMUWPUWRXPUGZGSUEZUWPVVNSUEZVVMUWOUWQX
      PUGZRQZVVOGSVVMUWOTPZUWQTPZUWQYOUQZVVSVVOVLVVMUWMTPUWNTPVVTVVMUWKFUXBVKZQ
      ZUWMTVVMVVJVWDUWMVLAVVIVVJUWLXQZUWKUXBFWRVQVVMVWDVVMUXBOVWCVAZVVJVWDOPAVW
      FVVKUWLAUYKVWCUYLPZVWFVNMUXBOVWCWAVOXRZVWEUXBOUWKVWCXSXTYAYBVVMUWJVWCQZUW
      NTVVMVVIVWIUWNVLAVVIVVJUWLYDZUWJUXBFWRVQVVMVWIVVMVWFVVIVWIOPVWHVWJUXBOUWJ
      VWCXSXTYAYBUWMUWNYEXTZVVMUWQVVMUWKOPZUWJOPZUWQOPVVMUXBOUWKAUXBOUOZVVKUWLA
      UYNUYOVWNHIDEYFXTXRZVWEYGZVVMUXBOUWJVWOVWJYGZUWKUWJYHXTYAZVVMUWQYPPZVWBVV
      MUWLVWSVVLUWLYIZVVMVWMVWLUWLVWSYJVWQVWPUWJUWKYKXTYLUWQYMVQZUWOUWQYNWBVVMV
      VSUXFGSVVMUXGUXHUXMVVSUXEPZVVSUXFSUEUXGVVMUXPVFAUXHVVKUWLVUBXRAUXMVVKUWLV
      VGXRVVMUYQVVRUYRPZVVRUXDPZVXBUYQVVMUYSVFVVMVVRTUYRVVMVVTVWAVWBVVRTPVWKVWR
      VXAUWOUWQYQWBVUAUUGVVMUWKFUWJUWKUJUGZVKZQZUWJVXFQZUFUGZUWQXPUGZVVRUXDVVMV
      XIUWOUWQXPVVMVXGUWMVXHUWNUFVVMUWKVXEPZVXGUWMVLVVMVWMVWLUWJUWKSUEZVXKVWQVW
      PVVMUWLVXLVWTVVMVWMVWLUWLVXLUIVWQVWPUWJUWKUUAXTXLZUWJUWKUUBWBUWKVXEFWRVQV
      VMUWJVXEPZVXHUWNVLVVMVWMVWLVXLVXNVWQVWPVXMUWJUWKVTWBUWJVXEFWRVQUUCUUDVVMU
      XJVXFUMQZQZVXJVLZUBUWJUWKYRUGZURVXJUXDPZVVMUBUWJUWKVXFVWQVWPVWTVVMVWCVXEV
      KZVXFVXEOVMUGZVVMVXEUXBUOZVXTVXFVLVVMUYNUYOVVIVVJVYBAUYNVVKUWLHXRAUYOVVKU
      WLIXRVWJVWEUYNUYOWLVVKVYBDEUWJUWKUUEWDUUFZFVXEUXBUUHVQVVMUYKVYBVWGVXTVYAP
      ZUYKVVMVNVFVYCAVWGVVKUWLMXRUYKVYBWLVWGVYDUXBOVXEVWCUUIWDWEYBVVMVXOVHUXCVX
      RVKZVHZVXRVVMVXOVYEVVMVXOUXCVXEYRUSUUJQZUUKQQZVKZVYEVVMFVHZTFVAZVYJOUOZVX
      EOUOZVXOVYIVLVVMFTOUULUGPZVYKAVYNVVKUWLKXRZVYNVYKVYLTOFUUMUUNUUOZUUPVQVVM
      VYNVYLVYOVYNVYKVYLVYPUUSVQVVMVWMVWLVYMVWQVWPUWJUWKYFXTVYJVXEVYGFVYGUUQUUT
      WBVVMVYHVXRUXCVVMVWMVWLVYHVXRVLVWQVWPUWJUWKUURXTUVAYSZUVBVVMVXRUYBUOVYFVX
      RVLVVMVXRUXBUYBVVMVXRVXEUXBUWJUWKUVCVYCUVDZAUYCVVKUWLUYMXRUVEVXRUXCVRYTYS
      UVIVVMVXQVXSUBVXRVVLUWLUXJVXRPZVXQVXSUIVVLUWLVYSWLZWLZVXPUXDPVXQVXSWUAVXP
      UXJUXCQZUXDWUAVXPUXJVYEQZWUBVVLUWLVXPWUCVLVYSVVMUXJVXOVYEVYQUVFUVGVYSWUCW
      UBVLVVLUWLUXJVXRUXCWRUVHYSWUAUYAUYCUXJUXBPZWUBUXDPZUYAWUAUYGVFAUYCVVKVYTU
      YMXRVVLUWLVYSWUDVVMVXRUXBUXJVYRUVJUVKUYPWUDWUEUXBUXJUXCWCWDWEUVLVXPVXJUXD
      UVMXGUVSUVNXLYBUYQVXCWLVXDVXBUXDVVRRUVOWDWEUBUAUXEVVSUVPUVQNUVRXEVVMUWPOP
      ZUWIUWROPYOUWRUDUEWLZVVPVVQYJVVMVVTWUFVWKUWOUVTVQAUWIVVKUWLVVHXRZVVMUWRYP
      PZWUGVVMVWAVWBWUIVWRVXAUWQUWAXTZUWRUWBYTUWPGUWRUWCWBYLVVMUWRTPZGTPVVNUWSV
      LVVMWUIWUKWUJUWRUWDVQVVMGWUHYAUWRGUWEXTUWFYCUWGUWH $.
      $( [15-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip1.a $e |- ( ph -> A e. RR ) $.
    c1lip1.b $e |- ( ph -> B e. RR ) $.
    c1lip1.f $e |- ( ph -> F e. ( CC ^pm RR ) ) $.
    c1lip1.dv $e |- ( ph -> ( ( _D ` F ) |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    c1lip1.cn $e |- ( ph -> ( F |` ( A [,] B ) ) e.
        ( ( A [,] B ) -cn-> RR ) ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip1 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( cfv cmin co cabs cle cr cc0 wcel va vb cv cmul wbr cicc wral wrex wa c0
      clt wne 0re ne0i ax-mp ral0 wceq cxr wb rexr icc0 syl2anc biimpar raleqdv
      syl mpbiri ralrimivw r19.2z sylancr wi cdv cima csup adantr simpr cc cres
      cpm ccncf eqid c1liplem1 oveq1 breq2d imbi2d 2ralbidv rcla4ev weq w3o wss
      iccssre ad2antrr sseld anim12d imp lttri4 breq1 fveq2 oveq2d fveq2d oveq2
      breq12d imbi12d breq2 oveq1d rcla42v ad2antlr pm2.27 adantl syld 0nn0 a1i
      nn0ge0i fvres ad2antrl ax-resscn cncff simpl ffvelrn eqeltrrd recnd subid
      syl2an abs0 syl6eq ad3antrrr simprl sseldd simplr mul01 3brtr4d syl5ibcom
      wf eqtrd a1d ancoms ad2antll abssub recn biimpd embantd 3jaodan mpdan mpd
      ralrimdvva reximdva pm2.61ltlei ) ACUCZGMZBUCZGMZNOZPMZFUCZUUGUUINOZPMZUD
      OZQUEZCDEUFOZUGZBUURUGZFRUHZEDAEDUKUEZUIZRUJULZUUTFRUGUVASRTUVDUMRSUNUOUV
      CUUTFRUVCUUTUUSBUJUGUUSBUPUVCUUSBUURUJAUURUJUQZUVBADURTZEURTZUVEUVBUSADRT
      ZUVFHDUTVEAERTZUVGIEUTVEDEVAVBVCVDVFVGUUTFRVHVIADEQUEZUIZUAUCZUBUCZUKUEZU
      VMGMZUVLGMZNOZPMZUUMUVMUVLNOZPMZUDOZQUEZVJZUBUURUGUAUURUGZFRUHZUVAUVKPGVK
      MZUURVLVLRUKVMZRTUVNUVRUWGUVTUDOZQUEZVJZUBUURUGUAUURUGZUIUWEUVKUAUBDEGUWG
      AUVHUVJHVNAUVIUVJIVNAUVJVOAGVPRVROTUVJJVNAUWFUURVQUURRVSOZTUVJKVNAGUURVQZ
      UWLTZUVJLVNUWGVTWAUWDUWKFUWGRUUMUWGUQZUWCUWJUAUBUURUURUWOUWBUWIUVNUWOUWAU
      WHUVRQUUMUWGUVTUDWBWCWDWEWFVEUVKUWDUUTFRUVKUUMRTZUIZUWDUUQBCUURUURUWQUUIU
      URTZUUGUURTZUIZUIZUUIUUGUKUEZBCWGZUUGUUIUKUEZWHZUWDUUQVJZUXAUUIRTZUUGRTZU
      IZUXEUWQUWTUXIUWQUWRUXGUWSUXHUWQUURRUUIAUURRWIZUVJUWPAUVHUVIUXJHIDEWJVBZW
      KZWLUWQUURRUUGUXLWLWMWNZUUIUUGWOVEUXAUXBUXFUXCUXDUXAUXBUIUWDUXBUUQVJZUUQU
      WTUWDUXNVJUWQUXBUWCUXNUUIUVMUKUEZUVOUUJNOZPMZUUMUVMUUINOZPMZUDOZQUEZVJUAU
      BUUIUUGUURUURUABWGZUVNUXOUWBUYAUVLUUIUVMUKWPUYBUVRUXQUWAUXTQUYBUVQUXPPUYB
      UVPUUJUVONUVLUUIGWQWRWSUYBUVTUXSUUMUDUYBUVSUXRPUVLUUIUVMNWTWSWRXAXBUBCWGZ
      UXOUXBUYAUUQUVMUUGUUIUKXCUYCUXQUULUXTUUPQUYCUXPUUKPUYCUVOUUHUUJNUVMUUGGWQ
      XDWSUYCUXSUUOUUMUDUYCUXRUUNPUVMUUGUUINWBWSWRXAXBXEXFUXBUXNUUQVJUXAUXBUUQX
      GXHXIUXAUXCUIUUQUWDUXAUXCUUQUXAUUJUUJNOZPMZUUMUUIUUINOZPMZUDOZQUEUXCUUQUX
      ASSUYEUYHQSSQUEUXASXJXLXKUXAUYESPMZSUXAUYDSPUXAUUJVPTZUYDSUQUXAUUJUXAUUIU
      WMMZUUJRUWRUYKUUJUQUWQUWSUUIUURGXMXNUWQUURRUWMYLZUWRUYKRTUWTAUYLUVJUWPARV
      PWIUWNUYLXOLUURRUWMXPVIWKZUWRUWSXQUURRUUIUWMXRYBXSXTZUUJYAVEWSYCYDUXAUYHU
      UMSUDOZSUXAUYGSUUMUDUXAUYGUYISUXAUYFSPUXAUUIVPTZUYFSUQUXAUUIUXAUURRUUIAUX
      JUVJUWPUWTUXKYEUWQUWRUWSYFYGXTUUIYAVEWSYCYDWRUXAUUMVPTUYOSUQUXAUUMUVKUWPU
      WTYHXTUUMYIVEYMYJUXCUYEUULUYHUUPQUXCUYDUUKPUXCUUJUUHUUJNUUIUUGGWQXDWSUXCU
      YGUUOUUMUDUXCUYFUUNPUUIUUGUUINWBWSWRXAYKWNYNUXAUXDUIZUWDUXDUUJUUHNOZPMZUU
      MUUIUUGNOZPMZUDOZQUEZVJZUUQUWTUWDVUDVJZUWQUXDUWSUWRVUEUWCVUDUUGUVMUKUEZUV
      OUUHNOZPMZUUMUVMUUGNOZPMZUDOZQUEZVJUAUBUUGUUIUURUURUACWGZUVNVUFUWBVULUVLU
      UGUVMUKWPVUMUVRVUHUWAVUKQVUMUVQVUGPVUMUVPUUHUVONUVLUUGGWQWRWSVUMUVTVUJUUM
      UDVUMUVSVUIPUVLUUGUVMNWTWSWRXAXBUBBWGZVUFUXDVULVUCUVMUUIUUGUKXCVUNVUHUYSV
      UKVUBQVUNVUGUYRPVUNUVOUUJUUHNUVMUUIGWQXDWSVUNVUJVUAUUMUDVUNVUIUYTPUVMUUIU
      UGNWBWSWRXAXBXEYOXFUYQUXDVUCUUQUXAUXDVOUYQVUCUUQUYQUYSUULVUBUUPQUXAUYSUUL
      UQZUXDUXAUYJUUHVPTVUOUYNUXAUUHUXAUUGUWMMZUUHRUWSVUPUUHUQUWQUWRUUGUURGXMYP
      UWQUYLUWSVUPRTUWTUYMUWRUWSVOUURRUUGUWMXRYBXSXTUUJUUHYQVBVNUYQVUAUUOUUMUDU
      XAVUAUUOUQZUXDUXAUXIVUQUXMUXGUYPUUGVPTVUQUXHUUIYRUUGYRUUIUUGYQYBVEVNWRXAY
      SYTXIUUAUUBUUDUUEUUCIHUUF $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip2.a $e |- ( ph -> A e. RR ) $.
    c1lip2.b $e |- ( ph -> B e. RR ) $.
    c1lip2.f $e |- ( ph -> F e. ( C^n ` 1 ) ) $.
    c1lip2.rn $e |- ( ph -> ran F C_ RR ) $.
    c1lip2.dm $e |- ( ph -> ( A [,] B ) C_ dom F ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip2 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( va c1 wcel cc cr co syl cc0 ccpn cfv cpm cdvn cdm ccncf cn0 wa wb elcpn
      1nn0 ax-mp simplbi wss cicc cdv ax-resscn a1i caddc ax-1cn addid2i oveq1i
      cres wceq 0nn0 dvnp1 sylancl syl5eqr dvn0 fveq2d simprbi eqeltrrd cv wral
      eqtrd wi ssid cle wbr nn0ge0i cpnord mp3an sseldi crn adantr pmfun fvelrn
      wfun sylan sseldd ralrimiva cncffvrn syl3anc mpd cncff sylancr cnex elpm2
      reex dvfre syl2anc fdm eleq2d biimpar ffvelrn rescncf imp syl21anc c1lip1
      wf ) ABCDEFGHIAGNUAUBZOZGPQUCROZJXLXMNGUDRZGUEZPUFRZOZNUGOZXLXMXQUHUIUKGN
      UJULZUMSZAQPUNZDEUORZXOUNZGUPUBZXOQUFRZOZYDYBVCYBQUFRZOZYAAUQURZLAYDXPOZY
      FAXNYDXPAXNTGUDRZUPUBZYDAXNTNUSRZGUDRZYLYMNGUDNUTVAVBAXMTUGOZYNYLVDXTVEGT
      VFVGVHAYKGUPAXMYKGVDXTGVISZVJVOAXLXQJXLXMXQXSVKSVLZAPPUNZYAMVMZYDUBQOZMXO
      VNYJYFVPYRAPVQZURZYIAYTMXOAYSXOOZUHZYDUEZQYDXJZYSUUEOZYTAUUFUUCAXOQGXJZXO
      QUNZUUFAYAGYEOZUUHUQAGXPOZUUJAYKGXPYPAGTUAUBZOZYKXPOZAXKUULGYOXRTNVRVSXKU
      ULUNVEUKNUKVTTNWAWBJWCUUMXMUUNYOUUMXMUUNUHUIVEGTUJULVKSVLAYRYAYSGUBZQOZMX
      OVNUUKUUJVPUUBYIAUUPMXOUUDGWDZQUUOAUUQQUNUUCKWEAGWHZUUCUUOUUQOAXMUURXTPQG
      WFSYSGWGWIWJWKMXOPQGWLWMWNZXOQGWOWPAXMUUIXTXMXOPGXJUUIPQGWQWSWRVKSXOGWTXA
      WEAUUGUUCAUUEXOYSAYRYJUUEXOVDZUUAYQYRYJUHXOPYDXJUUTXOPYDWOXOPYDXBSWPXCXDU
      UEQYSYDXEXAWKMXOPQYDWLWMWNYAYCUHZYFYHXOQYBYDXFXGXHAYAYCUUJGYBVCYGOZYILUUS
      UVAUUJUVBXOQYBGXFXGXHXI $.
      $( [16-Nov-2014] $)
  $}

  ${
    $d ph x y k a b $.  $d A x y k a b $.  $d B x y k a b $.  $d F x y k a b $.
    c1lip3.a $e |- ( ph -> A e. RR ) $.
    c1lip3.b $e |- ( ph -> B e. RR ) $.
    c1lip3.f $e |- ( ph -> ( F |` RR ) e. ( C^n ` 1 ) ) $.
    c1lip3.rn $e |- ( ph -> ( F " RR ) C_ RR ) $.
    c1lip3.dm $e |- ( ph -> ( A [,] B ) C_ dom F ) $.
    $( C1 functions are Lipschitz continuous on closed intervals.  (Contributed
       by Stefan O'Rear, 16-Nov-2014.) $)
    c1lip3 $p |- ( ph -> E. k e. RR A. x e. ( A [,] B ) A. y e. ( A [,] B )
        ( abs ` ( ( F ` y ) - ( F ` x ) ) ) <_
          ( k x. ( abs ` ( y - x ) ) ) ) $=
      ( cr cfv cmin co cabs wral wcel wa cres cmul cle wbr cicc wrex crn df-ima
      cima syl5eqssr cdm cin wss iccssre syl2anc ssin bicomi sylanbrc syl6sseqr
      cv dmres c1lip2 sseld anim12d imp oveqan12rd fveq2d breq1d biimpd anassrs
      wi fvres syl ralimdva reximdv mpd ) ACUTZGMUAZNZBUTZVRNZOPZQNZFUTVQVTOPQN
      UBPZUCUDZCDEUEPZRZBWFRZFMUFVQGNZVTGNZOPZQNZWDUCUDZCWFRZBWFRZFMUFABCDEFVRH
      IJAVRUGGMUIMGMUHKUJAWFMGUKZULZVRUKAWFMUMZWFWPUMZWFWQUMZADMSEMSWRHIDEUNUOZ
      LWRWSTWTWFMWPUPUQURGMVAUSVBAWHWOFMAWGWNBWFAVTWFSZTWEWMCWFAXBVQWFSZWEWMVKZ
      AXBXCTZTVTMSZVQMSZTZXDAXEXHAXBXFXCXGAWFMVTXAVCAWFMVQXAVCVDVEXHWEWMXHWCWLW
      DUCXHWBWKQXGXFVSWIWAWJOVQMGVLVTMGVLVFVGVHVIVMVJVNVNVOVP $.
      $( [16-Nov-2014] $)
  $}

  ${
    $( Membership in a closed real interval.  (Contributed by Stefan O'Rear,
       16-Nov-2014.) $)
    elicc4 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* ) ->
        ( C e. ( A [,] B ) <-> ( A <_ C /\ C <_ B ) ) ) $=
      ( cxr wcel w3a cicc co cle wbr wa wb elicc1 3adant3 3anass baibr 3ad2ant3
      bitr4d ) ADEZBDEZCDEZFCABGHEZUAACIJZCBIJZFZUCUDKZSTUBUELUAABCMNUASUFUELTU
      EUAUFUAUCUDOPQR $.
      $( [16-Nov-2014] $)

    $( Membership in a symmetric closed real interval.  (Contributed by Stefan
       O'Rear, 16-Nov-2014.) $)
    elicc4abs $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
        ( C e. ( ( A - B ) [,] ( A + B ) ) <-> ( abs ` ( C - A ) ) <_ B ) ) $=
      ( cr wcel w3a cmin co caddc cicc cle wbr wa cabs cfv cxr 3adant3 rexr syl
      wb resubcl readdcl 3ad2ant3 elicc4 syl3anc absdifle 3coml bitr4d ) ADEZBD
      EZCDEZFZCABGHZABIHZJHEZUMCKLCUNKLMZCAGHNOBKLZULUMPEZUNPEZCPEZUOUPTULUMDEZ
      URUIUJVAUKABUAQUMRSULUNDEZUSUIUJVBUKABUBQUNRSUKUIUTUJCRUCUMUNCUDUEUKUIUJU
      QUPTCABUFUGUH $.
      $( [16-Nov-2014] $)
  $}

