$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Diophantine sets 6 miscellanea
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d G a $.  $d F a $.  $d X a $.  $d A a $.
    $( Function value of a composition.  Like ~ fvco2 but with no restrictions
       on ` F ` .  Possibly a replacement for ~ fvco2 ?  (Contributed by Stefan
       O'Rear, 16-Oct-2014.) $)
    fvco4 $p |- ( ( G Fn A /\ X e. A ) -> ( ( F o. G ) ` X ) =
        ( F ` ( G ` X ) ) ) $=
      ( va wfn wcel wa ccom csn cima cv wceq cab cuni imaco fnsnfv eqcomd df-fv
      cfv imaeq2d syl5eq eqeq1d abbidv unieqd 3eqtr4g ) CAFDAGHZBCIZDJZKZELJZMZ
      ENZOBDCTZJZKZUKMZENZODUHTUNBTUGUMURUGULUQEUGUJUPUKUGUJBCUIKZKUPBCUIPUGUSU
      OBUGUOUSADCQRUAUBUCUDUEEDUHSEUNBSUF $.
      $( [16-Oct-2014] $)
  $}

  ${
    $d A a $.

    $( A finite set of positive integers is a set of positive integers.
       (Contributed by Stefan O'Rear, 16-Oct-2014.) $)
    fz1ssnn $p |- ( 1 ... A ) C_ NN $=
      ( va c1 cfz co cn cv elfznn ssriv ) BCADEFBGAHI $.
      $( [16-Oct-2014] $)
  $}

  ${
    ftp.a $e |- A e. _V $.
    ftp.b $e |- B e. _V $.
    ftp.c $e |- C e. _V $.
    ftp.d $e |- X e. _V $.
    ftp.e $e |- Y e. _V $.
    ftp.f $e |- Z e. _V $.
    ftp.g $e |- A =/= B $.
    ftp.h $e |- A =/= C $.
    ftp.i $e |- B =/= C $.
    $( A function with a domain of three elements.  (Contributed by Stefan
       O'Rear, 17-Oct-2014.) $)
    ftp $p |- { <. A , X >. , <. B , Y >. , <. C , Z >. } : { A , B , C } -->
        { X , Y , Z } $=
      ( ctp cop wf cpr wceq csn cun wa cin wne fpr ax-mp eqid1 fsn mpbir pm3.2i
      c0 wcel wn wo necomi df-ne mpbi pm3.2ni elpr mtbir disjsn fun mp2an df-tp
      feq1i feq23i bitri ) ABCPZDEFPZADQZBEQZCFQZPZRZABSZCUAZUBZDESZFUAZUBZVKVL
      SZVMUAZUBZRZVPVSWBRZVQVTWCRZUCVPVQUDULTZWEWFWGABUEWFMABDEGHJKUFUGWGWCWCTW
      CUHCFWCILUIUJUKWHCVPUMZUNWICATZCBTZUOWJWKCAUEWJUNACNUPCAUQURCBUEWKUNBCOUP
      CBUQURUSCABIUTVAVPCVBUJVPVQVSVTWBWCVCVDVOVIVJWDRWEVIVJVNWDVKVLVMVEVFVIVJV
      RWAWDABCVEDEFVEVGVHUJ $.
      $( [17-Oct-2014] $)
  $}

