$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Diophantine sets 6 miscellanea
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a $.

    $( A finite set of positive integers is a set of positive integers.
       (Contributed by Stefan O'Rear, 16-Oct-2014.) $)
    fz1ssnn $p |- ( 1 ... A ) C_ NN $=
      ( va c1 cfz co cn cv elfznn ssriv ) BCADEFBGAHI $.
      $( [16-Oct-2014] $)
  $}

  ${
    ftp.a $e |- A e. _V $.
    ftp.b $e |- B e. _V $.
    ftp.c $e |- C e. _V $.
    ftp.d $e |- X e. _V $.
    ftp.e $e |- Y e. _V $.
    ftp.f $e |- Z e. _V $.
    ftp.g $e |- A =/= B $.
    ftp.h $e |- A =/= C $.
    ftp.i $e |- B =/= C $.
    $( A function with a domain of three elements.  (Contributed by Stefan
       O'Rear, 17-Oct-2014.) $)
    ftp $p |- { <. A , X >. , <. B , Y >. , <. C , Z >. } : { A , B , C } -->
        { X , Y , Z } $=
      ( ctp cop wf cpr wceq csn cun wa cin c0 wne fpr ax-mp eqid fsn mpbir wcel
      pm3.2i wn wo necomi df-ne mpbi pm3.2ni mtbir disjsn fun mp2an df-tp feq1i
      elpr feq23i bitri ) ABCPZDEFPZADQZBEQZCFQZPZRZABSZCUAZUBZDESZFUAZUBZVKVLS
      ZVMUAZUBZRZVPVSWBRZVQVTWCRZUCVPVQUDUETZWEWFWGABUFWFMABDEGHJKUGUHWGWCWCTWC
      UICFWCILUJUKUMWHCVPULZUNWICATZCBTZUOWJWKCAUFWJUNACNUPCAUQURCBUFWKUNBCOUPC
      BUQURUSCABIVFUTVPCVAUKVPVQVSVTWBWCVBVCVOVIVJWDRWEVIVJVNWDVKVLVMVDVEVIVJVR
      WAWDABCVDDEFVDVGVHUK $.
      $( [17-Oct-2014] $)
  $}

