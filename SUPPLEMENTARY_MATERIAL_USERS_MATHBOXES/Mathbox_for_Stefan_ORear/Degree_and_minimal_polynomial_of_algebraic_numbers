$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Degree and minimal polynomial of algebraic numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c degAA minPolyAA $.

  $( Extend class notation to include the degree function for algebraic
     numbers. $)
  cdgraa $a class degAA $.

  $( Extend class notation to include the minimal polynomial for an algebraic
     number. $)
  cmpaa $a class minPolyAA $.

  ${
    $d x d p $.

    $( Define the degree of an algebraic number as the smallest degree of any
       nonzero polynomial which has said number as a root. $)
    df-dgraa $a |- degAA = ( x e. AA |-> sup ( { d e. NN |
        E. p e. ( ( Poly ` QQ ) \ { 0p } ) ( ( deg ` p ) = d /\
          ( p ` x ) = 0 ) } , RR , `' < ) ) $.

    $( Define the minimal polynomial of an algebraic number as the unique monic
       polynomial which achieves the minimum of ` degAA ` . $)
    df-mpaa $a |- minPolyAA = ( x e. AA |-> ( iota_ p e. ( Poly ` QQ )
        ( ( deg ` p ) = ( degAA ` x ) /\ ( p ` x ) = 0 /\
          ( ( coeff ` p ) ` ( degAA ` x ) ) = 1 ) ) ) $.
  $}

  $(
  @{
    @d x A a b @.  @d x B a b @.
    plydeg1.a @e |- F = ( x e. CC |-> ( ( A x. x ) + B ) ) @.
    @( A polynomial with 2 terms. @)
    plydeg1a @p |- ( ( A e. S /\ B e. S /\ S C_ CC ) -> F e. ( Poly ` S ) ) @=
      ? @.

    @( Degree of a polynomial with 2 terms. @)
    plydeg1b @p |- ( ( A e. CC /\ B e. CC /\ A =/= 0 ) -> ( deg ` A ) = 1 ) @=
      ? @.
  @}
  $)

  ${
    $d A x d p a b c $.  $d P x d p a b c $.

    $( Value of the degree function on an algebraic number.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    dgraaval $p |- ( A e. AA -> ( degAA ` A ) = sup ( { d e. NN |
        E. p e. ( ( Poly ` QQ ) \ { 0p } ) ( ( deg ` p ) = d /\
          ( p ` A ) = 0 ) } , RR , `' < ) ) $=
      ( va cv cdgr cfv wceq cc0 wa cq cply c0p wrex cn crab cr clt csup wor csn
      cdif ccnv caa cdgraa fveq2 eqeq1d anbi2d rexbidv rabbidv supeq1d df-dgraa
      ltso cnvso mpbi supex fvmpt ) DABEZFGCEHZDEZURGZIHZJZBKLGMUAUBZNZCOPZQRUC
      ZSUSAURGZIHZJZBVDNZCOPZQVGSUDUEUTAHZQVFVLVGVMVEVKCOVMVCVJBVDVMVBVIUSVMVAV
      HIUTAURUFUGUHUIUJUKDBCULQVLVGQRTQVGTUMQRUNUOUPUQ $.
      $( [25-Nov-2014] $)

    $( Properties of the degree of an algebraic number. $)
    dgraalem $p |- ( A e. AA -> ( ( degAA ` A ) e. NN /\
        E. p e. ( ( Poly ` QQ ) \ { 0p } ) ( ( deg ` p ) = ( degAA ` A ) /\
          ( p ` A ) = 0 ) ) ) $=
      ( va vb wcel cfv cv cdgr wceq cc0 wa cq c0p cn c1 wne eqeq2 anbi1d eqeq1d
      wrex caa cdgraa cply csn cdif crab cr clt ccnv dgraaval cuz wss c0 ssrab2
      csup nnuz sseqtri cc eldifsn biimpi ad2antrr simpr simplr syl12anc simpll
      wi dgrnznn eqid jctil weq fveq2 anbi12d rcla42ev syl3anc rexlimiva impcom
      fveq1 elqaa rabn0 3imtr4i infmssuzcl sylancr eqeltrd rexbidv elrab sylib
      ex ) AUAEZAUBFZBGZHFZCGZIZAWJFZJIZKZBLUCFZMUDUEZTZCNUFZEWINEWKWIIZWOKZBWR
      TZKWHWIWTUGUHUIUOZWTABCUJWHWTOUKFZULWTUMPZXDWTEWTNXEWSCNUNUPUQAUREZADGZFZ
      JIZDWRTZKWSCNTZWHXFXKXGXLXJXGXLVFDWRXHWREZXJKZXGXLXNXGKZXHHFZNEZXMXPXPIZX
      JKZXLXOXHWQEXHMPKZXGXJXQXMXTXJXGXMXTXHWQMUSUTVAXNXGVBXMXJXGVCZAXHLVGVDXMX
      JXGVEXOXJXRYAXPVHVIWPXSWKXPIZWOKCBXPXHNWRWLXPIWMYBWOWLXPWKQRBDVJZYBXRWOXJ
      YCWKXPXPWJXHHVKSYCWNXIJAWJXHVQSVLVMVNWGVOVPADVRWSCNVSVTWTOWAWBWCWSXCCWINW
      LWIIZWPXBBWRYDWMXAWOWLWIWKQRWDWEWF $.
      $( [25-Nov-2014] $)

    $( Closure of the degree function on algebraic numbers.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    dgraacl $p |- ( A e. AA -> ( degAA ` A ) e. NN ) $=
      ( va caa wcel cdgraa cfv cn cv cdgr wceq cc0 wa cq cply c0p csn cdif wrex
      dgraalem simpld ) ACDAEFZGDBHZIFUAJAUBFKJLBMNFOPQRABST $.
      $( [25-Nov-2014] $)

    $( Degree function on algebraic numbers is a function.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    dgraaf $p |- degAA : AA --> NN $=
      ( va vp vb caa cn cdgraa wf wfn cv cfv wcel wral cdgr wceq cc0 wa clt cvv
      cr wor cq cply c0p csn cdif wrex crab ccnv csup df-dgraa fnmpt ltso cnvso
      mpbi supex a1i mprg dgraacl rgen ffnfv mpbir2an ) DEFGFDHZAIZFJEKZADLBIZM
      JCINVCVEJONPBUAUBJUCUDUEUFCEUGZSQUHZUIZRKZVBADADVHFRABCUJUKVIVCDKSVFVGSQT
      SVGTULSQUMUNUOUPUQVDADVCURUSADEFUTVA $.
      $( [25-Nov-2014] $)

    $( Upper bound on degree of an algebraic number.  (Contributed by Stefan
       O'Rear, 25-Nov-2014.) $)
    dgraaub $p |- ( ( ( P e. ( Poly ` QQ ) /\ P =/= 0p ) /\ ( A e. CC /\
          ( P ` A ) = 0 ) ) -> ( degAA ` A ) <_ ( deg ` P ) ) $=
      ( vb va cq cfv wcel c0p wa cc0 wceq cv cdgr wrex cle fveq1 eqeq1d rcla4ev
      cn syl2anc cply wne cdgraa csn cdif crab clt ccnv csup caa simprl eldifsn
      cc cr biimpri adantr simprr elqaa sylanbrc dgraaval syl c1 cuz wss ssrab2
      wbr nnuz sseqtri dgrnznn eqid jctil fveq2 anbi12d eqeq2 anbi1d infmssuzle
      rexbidv elrab sylancr eqbrtrd ) BEUAFZGBHUBIZAUMGZABFZJKZIZIZAUCFZCLZMFZD
      LZKZAWIFZJKZIZCWAHUDUEZNZDSUFZUNUGUHUIZBMFZOWGAUJGZWHWSKWGWCAWKFZJKZDWPNZ
      XAWBWCWEUKWGBWPGZWEXDWBXEWFXEWBBWAHULUOUPZWBWCWEUQZXCWEDBWPWKBKXBWDJAWKBP
      QRTADURUSACDUTVAWGWRVBVCFZVDWTWRGZWSWTOVFWRSXHWQDSVEVGVHWGWTSGWJWTKZWNIZC
      WPNZXIABEVIWGXEWTWTKZWEIZXLXFWGWEXMXGWTVJVKXKXNCBWPWIBKZXJXMWNWEXOWJWTWTW
      IBMVLQXOWMWDJAWIBPQVMRTWQXLDWTSWKWTKZWOXKCWPXPWLXJWNWKWTWJVNVOVQVRUSWTWRV
      BVPVSVT $.
      $( [25-Nov-2014] $)

    $( A rational polynomial of degree less than an algebraic number cannot be
       zero at that number unless it is the zero polynomial.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    dgraa0p $p |- ( ( A e. AA /\ P e. ( Poly ` QQ ) /\
          ( deg ` P ) < ( degAA ` A ) ) -> ( ( P ` A ) = 0 <-> P = 0p ) ) $=
      ( caa wcel cq cply cfv cdgr cdgraa wbr cc0 wceq c0p wn simpl2 3syl simpl1
      wa cr syl clt w3a wne cle simpl3 wb cn0 dgrcl nn0re cn dgraacl nnre ltnle
      syl2anc mpbid cc simprl aacn dgraaub syl22anc expr mtod ex necon4ad 0pval
      simprr wi fveq1 eqeq1d syl5ibrcom 3ad2ant1 impbid ) ACDZBEFGDZBHGZAIGZUAJ
      ZUBZABGZKLZBMLZVRVTBMVRBMUCZVTNVRWBRZVTVPVOUDJZWCVQWDNZVMVNVQWBUEWCVOSDZV
      PSDZVQWEUFWCVNVOUGDWFVMVNVQWBOEBUHVOUIPWCVMVPUJDWGVMVNVQWBQAUKVPULPVOVPUM
      UNUOVRWBVTWDVRWBVTRZRZVNWBAUPDZVTWDVMVNVQWHOVRWBVTUQWIVMWJVMVNVQWHQAURZTV
      RWBVTVFABUSUTVAVBVCVDVMVNWAVTVGVQVMVTWAAMGZKLZVMWJWMWKAVETWAVSWLKABMVHVIV
      JVKVL $.
      $( [25-Nov-2014] $)

    $(
    @( Degree of a rational number. @)
    dgraaq @p |- ( A e. QQ -> ( degAA ` A ) = 1 ) @=
      ( va cq wcel cdgraa cfv c1 cle wbr wceq cc cneg cv cmul co caddc cmpt cc0
      oveq1d syl cdgr cply c0p wne qcn oveq2 eqid ovex fvmpt mulm1 negcl addcom
      syl2anc negid 3eqtrd eqtrd dgraaub syl22anc breqtrd cn wb caa qaa dgraacl
      nnle1eq1 mpbid ) ACDZAEFZGHIZVHGJZVGVHBKGLZBMZNOZAPOZQZUAFZGHVGVOCUBFDVOU
      CUDAKDZAVOFZRJVHVPHI??AUEZVGVRVKANOZAPOZRVGVQVRWAJVSBAVNWAKVOVLAJVMVTAPVL
      AVKNUFSVOUGVTAPUHUITVGWAALZAPOZAWBPOZRVGVTWBAPVGVQVTWBJVSAUJTSVGWBKDZVQWC
      WDJVGVQWEVSAUKTVSWBAULUMVGVQWDRJVSAUNTUOUPAVOUQUR?USVGVHUTDZVIVJVAVGAVBDW
      FAVCAVDTVHVETVF @.
      @( [25-Nov-2014] @)
    $)

    $( An algebraic number has exactly one monic polynomial of least degree.
       (Contributed by Stefan O'Rear, 25-Nov-2014.) $)
    mpaaeu $p |- ( A e. AA -> E! p e. ( Poly ` QQ ) ( ( deg ` p ) =
          ( degAA ` A ) /\ ( p ` A ) = 0 /\
        ( ( coeff ` p ) ` ( degAA ` A ) ) = 1 ) ) $=
      ( va wcel cdgr cfv wceq cc0 c1 cq wa c0p cc co cn0 syl syl2anc adantl cvv
      cmul vb vc caa cv cdgraa ccoe w3a cply wrex weq wi wral wreu csn dgraalem
      cdif cn simprd cdiv cxp cof wss qsscn wne wf eldifi ad2antlr cz 0z sselii
      zssq eqid coef2 sylancl dgrcl ffvelrn eldifsn simprbi wb dgreq0 necon3bid
      mpbid qreccl plyconst sylancr simpl simpr caddc qaddcl qmulcl coef3 reccl
      plymul recne0 dgrmulc syl3anc simprl aacn ad2antrr wfn ovex fnconstg mp1i
      eqtrd plyf ffn 3syl cnex inidm fvconst2 simplrr ofval mpdan mul01 coemulc
      a1i fveq1d dgraacl nnnn0 nn0ex simplrl eqcomd fveq2d recid2 3eqtrd eqeq1d
      fveq2 fveq1 3anbi123d rcla4ev syl13anc ex rexlimdva mpd cmin simp2 sylan2
      syl6eq clt wbr anim12i 0cnALT subid1i com12 impl simpll cneg 1z zq qnegcl
      mp2b plysub simprr1 simprl1 eqeltrd simprl3 simprr3 3eqtr4d dgrsub2 df-0p
      eqtr4d syl23anc breqtrd dgraa0p ofsubeq0 mp3an1 ralrimivva reu4 sylanbrc
      syl2an ) AUCDZBUDZEFZAUEFZGZAUVLFZHGZUVNUVLUFFZFZIGZUGZBJUHFZUIZUWACUDZEF
      ZUVNGZAUWDFZHGZUVNUWDUFFZFZIGZUGZKZBCUJZUKZCUWBULBUWBULUWABUWBUMUVKUWFUWH
      KZCUWBLUNZUPZUIZUWCUVKUVNUQDZUWSACUOURUVKUWPUWCCUWRUVKUWDUWRDZKZUWPUWCUXB
      UWPKZMIUWEUWIFZUSNZUNZUTZUWDTVAZNZUWBDZUXIEFZUVNGZAUXIFZHGZUVNUXIUFFZFZIG
      ZUWCUXCUXGUWBDZUWDUWBDZUXJUXCJMVBUXEJDZUXRVCUXCUXDJDZUXDHVDZUXTUXCOJUWIVE
      ZUWEODZUYAUXCUXSHJDUYCUXAUXSUVKUWPUWDUWBUWQVFVGZVHJHVKVIVJUWIJUWDUWIVLZVM
      VNUXCUXSUYDUYEJUWDVOPZOJUWEUWIVPQUXCUWDLVDZUYBUXAUYHUVKUWPUXAUXSUYHUWDUWB
      LVQVRVGUXCUXSUYHUYBVSUYEUXSUWDLUXDHUWIJUWDUWEUWEVLUYFVTWAPWBZUXDWCQUXEJWD
      WEUYEUXRUXSKZUAUBJUXGUWDUXRUXSWFUXRUXSWGUAUDZJDUBUDZJDKZUYKUYLWHNJDZUYJUY
      KUYLWIZRUYMUYKUYLTNJDZUYJUYKUYLWJZRWMQUXCUXKUWEUVNUXCUXEMDZUXEHVDZUXSUXKU
      WEGUXCUXDMDZUYBUYRUXCOMUWIVEZUYDUYTUXCUXSVUAUYEUWIJUWDUYFWKPZUYGOMUWEUWIV
      PQZUYIUXDWLQZUXCUYTUYBUYSVUCUYIUXDWNQUYEUXEJUWDWOWPUXBUWFUWHWQXDUXCUXMUXE
      HTNZHUXCAMDZUXMVUEGUVKVUFUXAUWPAWRZWSUXCMMUXEHTMUXGUWDSSAUXESDZUXGMWTUXCI
      UXDUSXAZMUXESXBXCUXCUXSMMUWDVEZUWDMWTZUYEJUWDXEZMMUWDXFZXGMSDZUXCXHXPZVUO
      MXIZVUFAUXGFUXEGUXCMUXEAVUIXJRUXBUWFUWHVUFXKXLXMUXCUYRVUEHGVUDUXEXNPXDUXC
      UXPUVNOUXFUTZUWIUXHNZFZUXEUXDTNZIUXCUVNUXOVURUXCUYRUXSUXOVURGVUDUYEUXEJUW
      DXOQXQUXCUVNODZVUSVUTGUXCUWTVVAUVKUWTUXAUWPAXRZWSUVNXSPUXCOOUXEUXDTOVUQUW
      ISSUVNVUHVUQOWTUXCVUIOUXESXBXCUXCVUAUWIOWTVUBOMUWIXFPOSDUXCXTXPZVVCOXIVVA
      UVNVUQFUXEGUXCOUXEUVNVUIXJRUXCVVAKZUVNUWEUWIVVDUWEUVNUXBUWFUWHVVAYAYBYCXL
      XMUXCUYTUYBVUTIGVUCUYIUXDYDQYEUWAUXLUXNUXQUGBUXIUWBUVLUXIGZUVOUXLUVQUXNUV
      TUXQVVEUVMUXKUVNUVLUXIEYGYFVVEUVPUXMHAUVLUXIYHYFVVEUVSUXPIVVEUVNUVRUXOUVL
      UXIUFYGXQYFYIYJYKYLYMYNUVKUWOBCUWBUWBUVKUVLUWBDZUXSKZKZUWMUWNVVHUWMKZUVLU
      WDYOVANZMHUNUTZGZUWNVVIVVJLVVKVVIAVVJFZHGZVVJLGZUVKVVGUWMVVNVVGUWMKUVKVVN
      UWMVVGUVQUWHKZUVKVVNUKUWAUVQUWLUWHUVOUVQUVTYPUWFUWHUWKYPUUAVVGVVPKZUVKVVN
      VVQUVKKVVMHHYONZHUVKVVQVUFVVMVVRGVUGVVQMMHHYOMUVLUWDSSAVVFUVLMWTZUXSVVPVV
      FMMUVLVEZVVSJUVLXEZMMUVLXFPWSUXSVUKVVFVVPUXSVUJVUKVULVUMPVGVUNVVQXHXPZVWB
      VUPVVGUVQUWHVUFYAVVGUVQUWHVUFXKXLYQHUUBUUCYRYLYQUUDUUEVVIUVKVVJUWBDZVVJEF
      ZUVNYSYTVVNVVOVSUVKVVGUWMUUFVVGVWCUVKUWMVVGUAUBJUVLUWDVVFUXSWFVVFUXSWGUYM
      UYNVVGUYORUYMUYPVVGUYQRIUUGJDZVVGIVHDIJDVWEUUHIUUIIUUJUUKXPUULVGVVIVWDUVM
      UVNYSVVIVVFUXSUWEUVMGUVMUQDUVMUVRFZUVMUWIFZGVWDUVMYSYTUVKVVFUXSUWMYAUVKVV
      FUXSUWMXKVVIUWEUVNUVMUWFUWHUWKUWAVVHUUMUVOUVQUVTUWLVVHUUNZUVAVVIUVMUVNUQV
      WHUVKUWTVVGUWMVVBWSUUOVVIUVSIVWFVWGUVOUVQUVTUWLVVHUUPVVIUVMUVNUVRVWHYCVVI
      VWGUWJIVVIUVMUVNUWIVWHYCUWFUWHUWKUWAVVHUUQXDUURJJUVLUWDUVMUVMVLUUSUVBVWHU
      VCAVVJUVDWPWBUUTYRVVGVVLUWNVSZUVKUWMVVFVVTVUJVWIUXSVWAVULVUNVVTVUJVWIXHMU
      VLUWDSUVEUVFUVJVGWBYLUVGUWAUWLBCUWBUWNUVOUWFUVQUWHUVTUWKUWNUVMUWEUVNUVLUW
      DEYGYFUWNUVPUWGHAUVLUWDYHYFUWNUVSUWJIUWNUVNUVRUWIUVLUWDUFYGXQYFYIUVHUVI
      $.
      $( [25-Nov-2014] $)

    $( Value of the minimal polynomial of an algebraic number.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    mpaaval $p |- ( A e. AA -> ( minPolyAA ` A ) = ( iota_ p e. ( Poly ` QQ )
        ( ( deg ` p ) = ( degAA ` A ) /\ ( p ` A ) = 0 /\
          ( ( coeff ` p ) ` ( degAA ` A ) ) = 1 ) ) ) $=
      ( va cv cdgr cfv cdgraa wceq cc0 ccoe c1 w3a cq cply crio caa cmpaa fveq2
      eqeq2d eqeq1d fveq2d 3anbi123d riotabidv df-mpaa riotaex fvmpt ) CABDZEFZ
      CDZGFZHZUIUGFZIHZUJUGJFZFZKHZLZBMNFZOUHAGFZHZAUGFZIHZUSUNFZKHZLZBUROPQUIA
      HZUQVEBURVFUKUTUMVBUPVDVFUJUSUHUIAGRZSVFULVAIUIAUGRTVFUOVCKVFUJUSUNVGUATU
      BUCCBUDVEBURUEUF $.
      $( [25-Nov-2014] $)

    $( Properties of the minimal polynomial of an algebraic number. $)
    mpaalem $p |- ( A e. AA -> ( ( minPolyAA ` A ) e. ( Poly ` QQ ) /\
        ( ( deg ` ( minPolyAA ` A ) ) = ( degAA ` A ) /\
          ( ( minPolyAA ` A ) ` A ) = 0 /\
          ( ( coeff ` ( minPolyAA ` A ) ) ` ( degAA ` A ) ) = 1 ) ) ) $=
      ( vp caa wcel cmpaa cfv cv cdgr cdgraa wceq cc0 ccoe c1 w3a cq cply fveq2
      crab wa eqeq1d crio mpaaval wreu mpaaeu riotacl2 syl eqeltrd fveq1 fveq1d
      3anbi123d elrab sylib ) ACDZAEFZBGZHFZAIFZJZAUOFZKJZUQUOLFZFZMJZNZBOPFZRZ
      DUNVEDUNHFZUQJZAUNFZKJZUQUNLFZFZMJZNZSUMUNVDBVEUAZVFABUBUMVDBVEUCVOVFDABU
      DVDBVEUEUFUGVDVNBUNVEUOUNJZURVHUTVJVCVMVPUPVGUQUOUNHQTVPUSVIKAUOUNUHTVPVB
      VLMVPUQVAVKUOUNLQUITUJUKUL $.
      $( [25-Nov-2014] $)

    $( Minimal polynomial is a polynomial.  (Contributed by Stefan O'Rear,
       25-Nov-2014.) $)
    mpaacl $p |- ( A e. AA -> ( minPolyAA ` A ) e. ( Poly ` QQ ) ) $=
      ( caa wcel cmpaa cfv cq cply cdgr cdgraa wceq cc0 ccoe w3a mpaalem simpld
      c1 ) ABCADEZFGECQHEAIEZJAQEKJRQLEEPJMANO $.
      $( [25-Nov-2014] $)

    $( Minimal polynomial has degree the degree of the number.  (Contributed by
       Stefan O'Rear, 25-Nov-2014.) $)
    mpaadgr $p |- ( A e. AA -> ( deg ` ( minPolyAA ` A ) ) = ( degAA ` A ) ) $=
      ( caa wcel cmpaa cfv cq cply cdgr cdgraa wceq cc0 ccoe w3a mpaalem simpr1
      c1 wa syl ) ABCADEZFGECZSHEAIEZJZASEKJZUASLEEPJZMQUBANTUBUCUDOR $.
      $( [25-Nov-2014] $)

    $( The minimal polynomial of an algebraic number has the number as a root.
       (Contributed by Stefan O'Rear, 25-Nov-2014.) $)
    mpaaroot $p |- ( A e. AA -> ( ( minPolyAA ` A ) ` A ) = 0 ) $=
      ( caa wcel cmpaa cfv cq cply cdgr cdgraa wceq cc0 ccoe w3a mpaalem simpr2
      c1 wa syl ) ABCADEZFGECZSHEAIEZJZASEKJZUASLEEPJZMQUCANTUBUCUDOR $.
      $( [25-Nov-2014] $)

    $( Minimal polynomial is monic.  (Contributed by Stefan O'Rear,
       25-Nov-2014.) $)
    mpaamn $p |- ( A e. AA -> ( ( coeff ` ( minPolyAA ` A ) ) `
          ( degAA ` A ) ) = 1 ) $=
      ( caa wcel cmpaa cfv cq cply cdgr cdgraa wceq cc0 ccoe w3a mpaalem simpr3
      c1 wa syl ) ABCADEZFGECZSHEAIEZJZASEKJZUASLEEPJZMQUDANTUBUCUDOR $.
      $( [25-Nov-2014] $)

    $(
    @( The minimal polynomial of a rational number. @)
    mpaaq @p |- ( A e. QQ -> ( minPolyAA ` A ) = ( x e. CC |-> ( x - A ) ) ) @=
      ? @.
    $)
  $}

