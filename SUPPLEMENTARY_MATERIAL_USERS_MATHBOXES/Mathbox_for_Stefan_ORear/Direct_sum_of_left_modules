$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Direct sum of left modules
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (+)m $.

  $( Class of module direct sum generator. $)
  cdsmm $a class (+)m $.

  ${
    $d s r f x $.
    $( The direct sum of a family of Abelian groups or left modules is the
       induced group structure on finite linear combinations of elements, here
       represented as functions with finite support. $)
    df-dsmm $a |- (+)m = ( s e. _V , r e. _V |-> ( ( s Xm_ r ) |`s
        { f e. X_ x e. dom r ( Base ` ( r ` x ) ) |
          { x e. dom r | ( f ` x ) =/= ( 0g ` ( r ` x ) ) } e. Fin } ) ) $.
  $}

  ${
    $d S s r f x $.  $d R s r f x $.  $d B s r $.
    $( The direct sum is a well-behaved binary operator.  (Contributed by
       Stefan O'Rear, 7-Jan-2015.) $)
    reldmdsmm $p |- Rel dom (+)m $=
      ( vs vr vx vf cvv cv cprdm cfv c0g wne cdm crab cfn wcel cixp cress cdsmm
      co cbs df-dsmm reldmmpt2 ) ABEEAFBFZGRCFZDFHUCUBHZIHJCUBKZLMNDCUEUDSHOLPR
      QCDABTUA $.
      $( [7-Jan-2015] $)

    dsmmval.b $e |- B = { f e. ( Base ` ( S Xm_ R ) ) |
        { x e. dom R | ( f ` x ) =/= ( 0g ` ( R ` x ) ) } e. Fin } $.
    $( Value of the module direct sum.  (Contributed by Stefan O'Rear,
       7-Jan-2015.) $)
    dsmmval $p |- ( R e. V -> ( S (+)m R ) = ( ( S Xm_ R ) |`s B ) ) $=
      ( vs vr wcel cvv co cprdm cress wceq cv cfv crab cbs c0 cdsmm c0g wne cdm
      elex cfn cixp oveq1 vex eqid prdmbas mp2an fveq2d rabeq syl oveq12d oveq2
      syl5eq dmeq fveq1 neeq2d rabeqbidv eleq1d syl6eqr df-dsmm ovmpt2 wn ress0
      ovex eqcomi reldmdsmm ovprc1 reldmprdm oveq1d 3eqtr4a adantr pm2.61ian )
      CFJCKJZDCUALZDCMLZBNLZOZCFUEDKJZVRWBHIDCKKHPZIPZMLZAPZEPQZWGWEQZUBQZUCZAW
      EUDZRZUFJZEAWLWISQUGZRZNLWAUADWEMLZWNEWQSQZRZNLWDDOZWFWQWPWSNWDDWEMUHZWTW
      OWROWPWSOWTWOWFSQZWRWDKJWEKJWOXBOHUIIUIAWFWEWDKKWFUJUKULWTWFWQSXAUMURWNEW
      OWRUNUOUPWECOZWQVTWSBNWECDMUQZXCWSWHWGCQZUBQZUCZACUDZRZUFJZEVTSQZRBXCWNXJ
      EWRXKXCWQVTSXDUMXCWMXIUFXCWKXGAWLXHWECUSXCWJXFWHXCWIXEUBWGWECUTUMVAVBVCVB
      GVDUPAEHIVEVTBNVIVFWCVGZWBVRXLTTBNLZVSWAXMTBVHVJDCUAVKVLXLVTTBNDCMVMVLVNV
      OVPVQUO $.
      $( [7-Jan-2015] $)

    $( Base set of the module direct sum.  (Contributed by Stefan O'Rear,
       7-Jan-2015.) $)
    dsmmbase $p |- ( R e. V -> B = ( Base ` ( S (+)m R ) ) ) $=
      ( wcel cvv cdsmm co cbs cfv wceq elex cprdm cress cv crab eqid fveq2d wss
      dsmmval c0g wne cdm cfn ssrab2 eqsstri ressbas2 ax-mp syl6reqr syl ) CFHC
      IHZBDCJKZLMZNCFOUNUPDCPKZBQKZLMZBUNUOURLABCDEIGUCUABUQLMZUBBUSNBARZERMVAC
      MUDMUEACUFSUGHZEUTSUTGVBEUTUHUIBUTURUQURTUTTUJUKULUM $.
      $( [7-Jan-2015] $)
  $}

  ${
    $d S s r f x $.  $d R s r f x $.  $d B s r $.
    dsmmval2.b $e |- B = ( Base ` ( S (+)m R ) ) $.
    $( Self-referential definition of the module direct sum.  (Contributed by
       Stefan O'Rear, 7-Jan-2015.) $)
    dsmmval2 $p |- ( S (+)m R ) = ( ( S Xm_ R ) |`s B ) $=
      ( vx vf cdsmm co cprdm cbs cfv cress cvv wcel wceq crab oveq2i 3eqtr4a c0
      eqid cv c0g wne cdm cfn ssrab2 ressbas2 ax-mp dsmmval fveq2d oveq2d ress0
      wn eqcomi reldmdsmm ovprc1 reldmprdm oveq1d pm2.61i ovprc2 oveq12d eqtr4i
      wss ) CBGHZCBIHZVDJKZLHZVEALHBMNZVDVGOVHVEEUAZFUAKZVIBKUBKUCEBUDPUENZFVEJ
      KZPZLHZVEVNJKZLHVDVGVMVOVELVMVLVCVMVOOVKFVLUFVMVLVNVEVNTVLTUGUHQEVMBCFMVM
      TUIZVHVFVOVELVHVDVNJVPUJUKRVHUMZCCGHZCCIHZVRJKZLHZVDVGCMNZVRWAOWBVSVJVICK
      UBKUCECUDPUENZFVSJKZPZLHZVSWFJKZLHVRWAWEWGVSLWEWDVCWEWGOWCFWDUFWEWDWFVSWF
      TWDTUGUHQEWECCFMWETUIZWBVTWGVSLWBVRWFJWHUJUKRWBUMZSSVTLHZVRWAWJSVTULUNCCG
      UOUPWIVSSVTLCCIUQUPURRUSCBGUTZVQVEVSVFVTLCBIUTVQVDVRJWKUJVARUSAVFVELDQVB
      $.
      $( [7-Jan-2015] $)
  $}

  ${
    $d S f x $.  $d R f x $.  $d P f x $.  $d I f x $.  $d V f x $.
    dsmmbas2.p $e |- P = ( S Xm_ R ) $.
    dsmmbas2.b $e |- B = { f e. ( Base ` P ) |
        dom ( f \ ( 0g o. R ) ) e. Fin } $.
    $( Base set of the direct sum module using the ~ fndmin abbreviation.
       (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    dsmmbas2 $p |- ( ( R Fn I /\ I e. V ) -> B = ( Base ` ( S (+)m R ) ) ) $=
      ( vx wfn wcel wa c0g cbs cfv crab wceq cvv c0 ccom cdif cdm cfn cdsmm wne
      cv co cprdm fveq2i rabeq ax-mp simpll fvco2 sylan neeq2d rabbidva eqid wn
      noel reldmprdm ovprc1 fveq2d cnx baseid str0 syl6eqr eleq2d mtbiri adantl
      con4i simplr simpr prdmbasfn wf fn0g mpbi biimpi fco sylancr ffn ad2antrr
      dffn2 syl fndmdif syl2anc fndm 3eqtr4d eleq1d syl5eq fnex dsmmbase eqtrd
      ) CFKZFGLZMZAEUGZNCUAZUBUCZUDLZEBOPZQZDCUEUHOPZIWPXBJUGZWQPZXDCPNPZUFZJCU
      CZQZUDLZEDCUIUHZOPZQZXCWPXBWTEXLQZXMXAXLRXBXNRBXKOHUJWTEXAXLUKULWPWTXJEXL
      WPWQXLLZMZWSXIUDXPXEXDWRPZUFZJFQZXGJFQZWSXIXPXRXGJFXPXDFLZMXQXFXEXPWNYAXQ
      XFRWNWOXOUMZFNCXDUNUOUPUQXPWQFKWRFKZWSXSRXPXLCDWQFSGXKXKURXLURXODSLZWPYDX
      OYDUSZXOWQTLWQUTYEXLTWQYEXLTOPTYEXKTODCUIVAVBVCOVDOPEEVEVFVGVHVIVKVJWNWOX
      OVLYBWPXOVMVNWNYCWOXOWNFSWRVOZYCWNSSNVOZFSCVOZYFNSKYGVPSNWCVQWNYHFCWCVRFS
      SNCVSVTFSWRWAWDWBJFWQWRWEWFWNXIXTRZWOXOWNXHFRYIFCWGXGJXHFUKWDWBWHWIUQWJWP
      CSLXMXCRFGCWKJXMCDESXMURWLWDWMWJ $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d S f x $.  $d R f x $.  $d P f x $.  $d I f x $.  $d V f x $.

    $( For finite products, the direct sum is just the module product.
       (Contributed by Stefan O'Rear, 1-Feb-2015.) $)
    dsmmfi $p |- ( ( R Fn I /\ I e. Fin ) -> ( S (+)m R ) = ( S Xm_ R ) ) $=
      ( vf wfn cfn wcel wa co cprdm cbs cfv cress eqid cdm wceq wss cvv ax-mp
      c0 cdsmm dsmmval2 cv c0g ccom cdif crab wral noel reldmprdm ovprc1 fveq2d
      wn cnx baseid str0 syl6eqr eleq2d mtbiri con4i adantl simplr simpll simpr
      prdmbasfn fndm syl eqeltrd difss dmss ssfi sylancl rabid2 sylibr dsmmbas2
      ralrimiva eqtr2d oveq2d ovex ressid syl6eq syl5eq ) ACEZCFGZHZBAUAIZBAJIZ
      WFKLZMIZWGWHABWHNUBWEWIWGWGKLZMIZWGWEWHWJWGMWEWJDUCZUDAUEZUFZOZFGZDWJUGZW
      HWEWPDWJUHWJWQPWEWPDWJWEWLWJGZHZWLOZFGWOWTQZWPWSWTCFWSWLCEWTCPWSWJABWLCRF
      WGWGNZWJNZWRBRGZWEXDWRXDUMZWRWLTGWLUIXEWJTWLXEWJTKLTXEWGTKBAJUJUKULKUNKLD
      DUOUPUQURUSUTVAWCWDWRVBZWCWDWRVCWEWRVDVECWLVFVGXFVHWNWLQXAWLWMVIWNWLVJSWT
      WOVKVLVPWPDWJVMVNWQWGABDCFXBWQNVOVQVRWGRGWKWGPBAJVSWJWGRXCVTSWAWB $.
      $( [1-Feb-2015] $)
  $}

  ${
    $d S a b $.  $d R a b $.  $d X a b $.  $d I a b $.
    dsmmelbas.p $e |- P = ( S Xm_ R ) $.
    dsmmelbas.c $e |- C = ( S (+)m R ) $.
    dsmmelbas.b $e |- B = ( Base ` P ) $.
    dsmmelbas.h $e |- H = ( Base ` C ) $.
    dsmmelbas.i $e |- ( ph -> I e. V ) $.
    dsmmelbas.r $e |- ( ph -> R Fn I ) $.
    $( Membership in the finitely supported hull of a structure product in
       terms of the index set.  (Contributed by Stefan O'Rear, 11-Jan-2015.) $)
    dsmmelbas $p |- ( ph -> ( X e. H <-> ( X e. B /\ { a e. I |
          ( X ` a ) =/= ( 0g ` ( R ` a ) ) } e. Fin ) ) ) $=
      ( wcel cfv cbs vb cv c0g wne cdm crab cfn cprdm co wa cdsmm cvv wceq fnex
      wfn syl2anc eqid dsmmbase syl fveq2i eqtri syl6reqr eleq2d neeq1d rabbidv
      fveq1 eleq1d elrab eqtr2i eleq2i a1i fndm rabeq 3syl anbi12d syl5bb bitrd
      wb ) AJGRJKUBZUAUBZSZVSESUCSZUDZKEUEZUFZUGRZUAFEUHUIZTSZUFZRZJBRZVSJSZWBU
      DZKHUFZUGRZUJZAGWIJAWIFEUKUIZTSZGAEULRZWIWRUMAEHUOZHIRWSQPHIEUNUPKWIEFUAU
      LWIUQURUSGCTSWROCWQTMUTVAVBVCWJJWHRZWMKWDUFZUGRZUJAWPWFXCUAJWHVTJUMZWEXBU
      GXDWCWMKWDXDWAWLWBVSVTJVFVDVEVGVHAXAWKXCWOXAWKVRAWHBJBDTSWHNDWGTLUTVIVJVK
      AXBWNUGAWTWDHUMXBWNUMQHEVLWMKWDHVMVNVGVOVPVQ $.
      $( [11-Jan-2015] $)
  $}

  ${
    $d ph a b $.  $d P a b $.  $d S a b $.  $d R a b $.  $d Z a b $.
    $d A a b $.  $d J a b $.  $d K a b $.  $d I a $.
    dsmmcl.p $e |- P = ( S Xm_ R ) $.
    dsmmcl.h $e |- H = ( Base ` ( S (+)m R ) ) $.
    dsmmcl.i $e |- ( ph -> I e. W ) $.
    dsmmcl.s $e |- ( ph -> S e. V ) $.
    dsmmcl.r $e |- ( ph -> R : I --> Mnd ) $.
    ${
      dsmm0cl.z $e |- Z = ( 0g ` P ) $.
      $( The all-zero vector is contained in the finite hull, since its support
         is empty and therefore finite.  This theorem along with the next one
         effectively proves that the finite hull is a "submonoid", although
         that does not exist as a defined concept yet.  (Contributed by Stefan
         O'Rear, 11-Jan-2015.) $)
      dsmm0cl $p |- ( ph -> Z e. H ) $=
        ( va wcel cfv c0g wceq cbs cv wne crab cfn cmnd prdmmndd mndidcl syl c0
        eqid wn wral wa ccom prdm0g syl6eqr adantr fveq1d wf fvco2 sylan eqtr3d
        wfn ffn sylibr ralrimiva rabeq0 0fin syl6eqel cdsmm dsmmelbas mpbir2and
        nne co ) AIEQIBUARZQZPUBZIRZVRCRSRZUCZPFUDZUEQABUFQVQACDFGHBJLMNUGVPBIV
        PUKZOUHUIAWBUJUEAWAULZPFUMWBUJTAWDPFAVRFQZUNZVSVTTWDWFVRSCUOZRZVSVTWFVR
        WGIAWGITWEAWGBSRIACDFGHBJLMNUPOUQURUSACFVDZWEWHVTTAFUFCUTWINFUFCVEUIZFS
        CVRVAVBVCVSVTVNVFVGWAPFVHVFVIVJAVPDCVKVOZBCDEFHIPJWKUKWCKLWJVLVM $.
        $( [11-Jan-2015] $)
    $}

    ${
      dsmmacl.j $e |- ( ph -> J e. H ) $.
      dsmmacl.k $e |- ( ph -> K e. H ) $.
      dsmmacl.a $e |- A = ( +g ` P ) $.
      $( The finite hull is closed under addition.  (Contributed by Stefan
         O'Rear, 11-Jan-2015.) $)
      dsmmacl $p |- ( ph -> ( J A K ) e. H ) $=
        ( wcel va co cbs cfv cv c0g wne crab cfn cmnd prdmmndd wa cdsmm eqid wf
        wfn ffn dsmmelbas mpbid simpld mndcl syl3anc cplusg simpr prdmplusgfval
        syl adantr neeq1d rabbidva cun wss simprd unfi syl2anc wo wceq wn sylan
        ffvelrn mndidcl mndlid oveq12 eqeq1d syl5ibrcom neorian con1bii 3imtr4g
        bicomi nne con4d ss2rabdv unrab syl6sseqr ssfi eqeltrd mpbir2and ) AHIB
        UBZFTWQCUCUDZTZUAUEZWQUDZWTDUDZUFUDZUGZUAGUHZUITACUJTHWRTZIWRTZWSADEGJK
        CLNOPUKAXFWTHUDZXCUGZUAGUHZUITZAHFTXFXKULQAWREDUMUBZCDEFGKHUALXLUNZWRUN
        ZMNAGUJDUOZDGUPZPGUJDUQVFZURUSZUTZAXGWTIUDZXCUGZUAGUHZUITZAIFTXGYCULRAW
        RXLCDEFGKIUALXMXNMNXQURUSZUTZWRBCHIXNSVAVBAXEXHXTXBVCUDZUBZXCUGZUAGUHZU
        IAXDYHUAGAWTGTZULZXAYGXCYKWRBDEHIGWTJKCLXNAEJTYJOVGAGKTYJNVGAXPYJXQVGAX
        FYJXSVGAXGYJYEVGSAYJVDVEVHVIAXJYBVJZUITZYIYLVKYIUITAXKYCYMAXFXKXRVLAXGY
        CYDVLXJYBVMVNAYIXIYAVOZUAGUHYLAYHYNUAGYKYNYHYKXHXCVPXTXCVPULZYGXCVPZYNV
        QYHVQYKYPYOXCXCYFUBZXCVPZYKXBUJTZXCXBUCUDZTZYRAXOYJYSPGUJWTDVSVRZYKYSUU
        AUUBYTXBXCYTUNZXCUNZVTVFYTYFXBXCXCUUCYFUNUUDWAVNYOYGYQXCXHXCXTXCYFWBWCW
        DYOYNYNYOVQXHXCXTXCWEWHWFYGXCWIWGWJWKXIYAUAGWLWMYLYIWNVNWOAWRXLCDEFGKWQ
        UALXMXNMNXQURWP $.
        $( [11-Jan-2015] $)
    $}
  $}

  ${
    $d x S $.  $d x R $.  $d x X $.  $d x Y $.  $d x I $.  $d x B $.
    $d ph x $.  $d J x $.
    prdminvgd2.y $e |- Y = ( S Xm_ R ) $.
    prdminvgd2.i $e |- ( ph -> I e. W ) $.
    prdminvgd2.s $e |- ( ph -> S e. V ) $.
    prdminvgd2.r $e |- ( ph -> R : I --> Grp ) $.
    prdminvgd2.b $e |- B = ( Base ` Y ) $.
    prdminvgd2.n $e |- N = ( invg ` Y ) $.
    prdminvgd2.x $e |- ( ph -> X e. B ) $.
    prdminvgd2.j $e |- ( ph -> J e. I ) $.
    $( Negation of a single coordinate in a structure product.  (Contributed by
       Stefan O'Rear, 11-Jan-2015.) $)
    prdminvgd2 $p |- ( ph -> ( ( N ` X ) ` J ) =
        ( ( invg ` ( R ` J ) ) ` ( X ` J ) ) ) $=
      ( cfv vx cminusg cmpt prdminvgd fveq1d wcel wceq fveq2d fveq12d eqid fvex
      cv fveq2 fvmpt syl eqtrd ) AFJGTZTFUAEUAULZJTZURCTZUBTZTZUCZTZFJTZFCTZUBT
      ZTZAFUQVCAUABCDEGHIJKLMNOPQRUDUEAFEUFVDVHUGSUAFVBVHEVCURFUGZUSVEVAVGVIUTV
      FUBURFCUMUHURFJUMUIVCUJVEVGUKUNUOUP $.
      $( [11-Jan-2015] $)
  $}

  ${
    $d H a b $.  $d P a b $.  $d S a b $.  $d R a b $.  $d I b $.  $d ph a b $.
    dsmmsubg.p $e |- P = ( S Xm_ R ) $.
    dsmmsubg.h $e |- H = ( Base ` ( S (+)m R ) ) $.
    dsmmsubg.i $e |- ( ph -> I e. W ) $.
    dsmmsubg.s $e |- ( ph -> S e. V ) $.
    dsmmsubg.r $e |- ( ph -> R : I --> Grp ) $.
    $( The finite hull of a product of groups is additionally closed under
       negation and thus is a subgroup of the product.  (Contributed by Stefan
       O'Rear, 11-Jan-2015.) $)
    dsmmsubg $p |- ( ph -> H e. ( SubGrp ` P ) ) $=
      ( va vb cfv wcel wceq cgrp eqid cplusg cress co c0g eqidd cdsmm cbs cprdm
      cv wne cdm crab cfn cvv wf fex syl2anc dsmmbase syl eqcomd wss ssrab2 a1i
      eqsstrd fveq2i syl6sseqr syl5eqss cmnd ssriv fss sylancl dsmm0cl 3ad2ant1
      grpmnd w3a simp2 simp3 dsmmacl wa cminusg prdmgrpd adantr sselda grpinvcl
      simpr wfn ffn dsmmelbas mpbid simprd ad2antrr prdminvgd2 adantrr ad2antll
      fveq2 ffvelrn sylan adantlr grpinvid expr necon3d ss2rabdv ssfi mpbir2and
      3eqtrd issubgrpd2 ) ANOEBUAPZBEUBUCZBBUDPZAXHUEAXIUEAXGUEAEDCUFUCZUGPZBUG
      PZJAXKDCUHUCZUGPZXLAXKOUIZNUIZPZXOCPZUDPZUJZOCUKULUMQZNXNULZXNAYBXKACUNQZ
      YBXKRAFSCUOZFHQZYCMKFSHCUPUQOYBCDNUNYBTURUSUTYBXNVAAYANXNVBVCVDBXMUGIVEVF
      VGZABCDEFGHXIIJKLAYDSVHVAFVHCUOZMNSVHXPVNVIFSVHCVJVKZXITVLAXPEQZXOEQZVOXG
      BCDEFXPXOGHIJAYIYEYJKVMAYIDGQZYJLVMAYIYGYJYHVMAYIYJVPAYIYJVQXGTVRAYIVSZXP
      BVTPZPZEQYNXLQZXOYNPZXSUJZOFULZUMQZYLBSQZXPXLQZYOAYTYIACDFGHBIKLMWAZWBAEX
      LXPYFWCZXLBYMXPXLTZYMTZWDUQYLXTOFULZUMQZYRUUFVAYSYLUUAUUGYLYIUUAUUGVSAYIW
      EYLXLXJBCDEFHXPOIXJTZUUDJAYEYIKWBZACFWFZYIAYDUUJMFSCWGUSWBZWHWIWJYLYQXTOF
      YLXOFQZVSZXQXSYPXSYLUULXQXSRZYPXSRYLUULUUNVSVSYPXQXRVTPZPZXSUUOPZXSYLUULY
      PUUPRUUNUUMXLCDFXOYMGHXPBIAYEYIUULKWKAYKYIUULLWKAYDYIUULMWKUUDUUEYLUUAUUL
      UUCWBYLUULWEWLWMUUNUUPUUQRYLUULXQXSUUOWOWNYLUULUUQXSRZUUNUUMXRSQZUURAUULU
      USYIAYDUULUUSMFSXOCWPWQWRXRUUOXSXSTUUOTWSUSWMXEWTXAXBUUFYRXCUQYLXLXJBCDEF
      HYNOIUUHUUDJUUIUUKWHXDUUBXF $.
      $( [11-Jan-2015] $)
  $}

  ${
    $d ph a b x $.  $d S a b x $.  $d R a b x $.  $d I a b x $.  $d P a b x $.
    $d U a b $.  $d H a b x $.
    dsmmlss.i $e |- ( ph -> I e. W ) $.
    dsmmlss.s $e |- ( ph -> S e. Ring ) $.
    dsmmlss.r $e |- ( ph -> R : I --> LMod ) $.
    dsmmlss.k $e |- ( ( ph /\ x e. I ) -> ( Scalar ` ( R ` x ) ) = S ) $.

    ${
      dsmmlss.p $e |- P = ( S Xm_ R ) $.
      dsmmlss.u $e |- U = ( LSubSp ` P ) $.
      dsmmlss.h $e |- H = ( Base ` ( S (+)m R ) ) $.
      $( The finite hull of a product of modules is additionally closed under
         scalar multiplication and thus is a linear subspace of the product.
         (Contributed by Stefan O'Rear, 11-Jan-2015.) $)
      dsmmlss $p |- ( ph -> H e. U ) $=
        ( wcel cfv clmod eqid va vb csubg cv cvsca co wral csca cbs crg wf cgrp
        wss lmodgrp ssriv fss sylancl dsmmsubg wa c0g wne crab prdmlmodd adantr
        cfn simprl simprr wb cdsmm wfn ffn syl dsmmelbas mpbid lmodvscl syl3anc
        simpld simprd wceq ad2antrr cvv fex syl2anc prdmsca fveq2d eleq2d simpr
        biimpar adantrr prdmvscafval ffvelrn sylan adantlr eqtrd eleqtrrd oveq2
        simplrl lmodvs0 eqeq1d syl5ibrcom impr expr necon3d ss2rabdv ralrimivva
        ssfi mpbir2and islss4 ) AGFQZGCUCRZQZUAUDZUBUDZCUERZUFZGQZUBGUGUACUHRZU
        IRZUGZACDEGHUJINPJKAHSDUKZSULUMHULDUKLUASULXLUNUOHSULDUPUQURAXPUAUBXRGA
        XLXRQZXMGQZUSZUSZXPXOCUIRZQZBUDZXORZYGDRZUTRZVAZBHVBZVEQZYDCSQZYAXMYEQZ
        YFAYNYCABDEHICNKJLMVCZVDAYAYBVFYDYOYGXMRZYJVAZBHVBZVEQZYDYBYOYTUSZAYAYB
        VGAYBUUAVHYCAYEEDVIUFZCDEGHIXMBNUUBTZYETZPJAXTDHVJZLHSDVKVLZVMVDVNZVQZX
        LXNXQXRYECXMUUDXQTZXNTZXRTZVOVPYDYTYLYSUMYMYDYOYTUUGVRYDYKYRBHYDYGHQZUS
        ZYQYJYHYJYDUULYQYJVSZYHYJVSYDUULUUNUSUSYHXLYQYIUERZUFZYJYDUULYHUUPVSUUN
        UUMYEDEXNXLXMHYGEUIRZUJICNUUDUUJUUQTAEUJQZYCUULKVTAHIQZYCUULJVTAUUEYCUU
        LUUFVTYDXLUUQQZUULAYAUUTYBAUUTYAAUUQXRXLAEXQUIAUURDWAQZEXQVSZKAXTUUSUVA
        LJHSIDWBWCCDEUJWANWDWCZWEWFWHWIVDYDYOUULUUHVDYDUULWGWJWIYDUULUUNUUPYJVS
        ZUUMUVDUUNXLYJUUOUFZYJVSZUUMYISQZXLYIUHRZUIRZQUVFAUULUVGYCAXTUULUVGLHSY
        GDWKWLWMUUMXLXRUVIAYAYBUULWQAUULUVIXRVSYCAUULUSZUVHXQUIUVJUVHEXQMAUVBUU
        LUVCVDWNWEWMWOUUOUVHUVIYIXLYJUVHTUUOTUVITYJTWRWCUUNUUPUVEYJYQYJXLUUOWPW
        SWTXAWNXBXCXDYSYLXFWCAXPYFYMUSVHYCAYEUUBCDEGHIXOBNUUCUUDPJUUFVMVDXGXEAY
        NXIXKXSUSVHYPXRFXNGXQXJYECUAUBUUIUUKUUDUUJOXJTXHVLXG $.
        $( [11-Jan-2015] $)
    $}

    ${
      dsmmlmod.c $e |- C = ( S (+)m R ) $.
      $( The direct sum of a family of modules is a module.  (Contributed by
         Stefan O'Rear, 11-Jan-2015.) $)
      dsmmlmod $p |- ( ph -> C e. LMod ) $=
        ( cprdm co clmod wcel cdsmm cbs cfv eqid prdmlmodd cress dsmmval2 eqtri
        clss dsmmlss lsslmod syl2anc ) AEDMNZOPEDQNZRSZUIUESZPCOPABDEFGUIUITZIH
        JKUAABUIDEULUKFGHIJKUMULTZUKTZUFULUKUICCUJUIUKUBNLUKDEUOUCUDUNUGUH $.
        $( [11-Jan-2015] $)
    $}
  $}

