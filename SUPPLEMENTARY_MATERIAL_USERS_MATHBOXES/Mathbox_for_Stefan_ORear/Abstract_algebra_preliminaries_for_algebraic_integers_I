$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Abstract algebra preliminaries for algebraic integers I
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    algpart.a $e |- A = ( { <. ( Base ` ndx ) , B >. , <. ( +g ` ndx ) , P >. ,
        <. ( .r ` ndx ) , T >. } u. { <. ( Scalar ` ndx ) , S >. ,
          <. ( vsca ` ndx ) , M >. } ) $.

    ${
      algpart.b $e |- D = ( { ( Base ` ndx ) , ( +g ` ndx ) , ( .r ` ndx ) } u.
          { ( Scalar ` ndx ) , ( vsca ` ndx ) } ) $.
      $( Lemma to shorten proofs of ~ algbase through ~ algvsca .  (Contributed
         by Stefan O'Rear, 27-Nov-2014.) $)
      algfn $p |- A Fn D $=
        ( va cnx cfv cun wcel c5 wceq wne ltneii neeqtrri c6 cbs cmulr ctp csca
        cplusg csn cop cvsca fvex eqid eqid1 rngfn w3o w3a 1re 5re 1lt5 basendx
        wn c1 c2 2re 2lt5 df-plusg 2nn ndxarg 3re 3lt5 df-mulr 3pm3.2i ne3anior
        c3 3nn mpbi df-sca eleq1i cn elexi eltp bitri mtbir fnunsn df-pr uneq2i
        5nn cpr unass 3eqtr4i 6re 1lt6 2lt6 3lt6 6nn 5lt6 elsnc nemtbir pm3.2ni
        wo df-vsca elun ) KUALZKUELZKUBLZUCZKUDLZUFZMZCXABUGXBDUGXCFUGUCZXEEUGZ
        UFZMZAKUHLZGKUHUIXDXGXHXKXEEKUDUIBXDDXHFXHUJXDUKULXKUJXGUJXEXDNZOXAPOXB
        POXCPUMZOXAQZOXBQZOXCQZUNXNUSXOXPXQOUTXAUTOUOUPUQRURSOVAXBVAOVBUPVCRUEV
        AJJVDVEVFZSOVLXCVLOVGUPVHRUBVLJJVIVMVFZSVJOXAOXBOXCVKVNXMOXDNXNXEOXDUDO
        JJVOWEVFZVPOXAXBXCOVQWEVRVSVTWAWBXHXIXLGUGZWFZMXHXJYAUFZMZMAXKYCMYBYDXH
        XIYAWCWDHXHXJYCWGWHXDXEXLWFZMXDXFXLUFZMZMCXGYFMYEYGXDXEXLWCWDIXDXFYFWGW
        HXLXGNZTXDNZTXFNZWRZYIYJYITXAPTXBPTXCPUMZTXAQZTXBQZTXCQZUNYLUSYMYNYOTUT
        XAUTTUOWIWJRURSTVAXBVATVBWIWKRXRSTVLXCVLTVGWIWLRXSSVJTXATXBTXCVKVNTXAXB
        XCTVQWMVRZVSWAYJTXETOXEOTUPWIWNRXTSTXEYPWOWPWQYHTXGNYKXLTXGUHTJJWSWMVFV
        PTXDXFWTVTWAWB $.
        $( [27-Nov-2014] $)
    $}

    $( Lemma to shorten proofs of ~ algbase through ~ algvsca .  (Contributed
       by Stefan O'Rear, 27-Nov-2014.) $)
    algfun $p |- Fun A $=
      ( cnx cbs cfv cplusg cmulr ctp csca cvsca cpr cun wfn wfun eqid algfn
      fnfun ax-mp ) AHIJHKJHLJMHNJHOJPQZRASABUDCDEFGUDTUAUDAUBUC $.
      $( [27-Nov-2014] $)

    $d A a $.  $d B a $.  $d P a $.  $d T a $.  $d S a $.  $d M a $.

    $( The base set of a constructed algebra.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    algbase $p |- ( B e. V -> B = ( Base ` A ) ) $=
      ( va cbs cnx cfv cop cplusg cmulr ctp csca cvsca cpr wcel cun cvv eqeltri
      tpex prex unex algfun baseid opex tpid1 elun1 ax-mp eleqtrri strfv ) BAJG
      IAKJLZBMZKNLCMZKOLEMZPZKQLDMZKRLFMZSZUAZUBHUSVBUPUQURUDUTVAUEUFUCABCDEFHU
      GIUHUPVCAUPUSTUPVCTUPUQURUOBUIUJUPUSVBUKULHUMUN $.
      $( [27-Nov-2014] $)

    $( The additive operation of a constructed algebra.  (Contributed by Stefan
       O'Rear, 27-Nov-2014.) $)
    algaddg $p |- ( P e. V -> P = ( +g ` A ) ) $=
      ( va cplusg cnx cbs cfv cop cmulr ctp csca cvsca cpr wcel cun cvv eqeltri
      tpex prex unex algfun plusgid opex tpid2 elun1 ax-mp eleqtrri strfv ) CAJ
      GIAKLMBNZKJMZCNZKOMENZPZKQMDNZKRMFNZSZUAZUBHUSVBUOUQURUDUTVAUEUFUCABCDEFH
      UGIUHUQVCAUQUSTUQVCTUOUQURUPCUIUJUQUSVBUKULHUMUN $.
      $( [27-Nov-2014] $)

    $( The multiplicative operation of a constructed algebra.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    algmulr $p |- ( T e. V -> T = ( .r ` A ) ) $=
      ( va cmulr cnx cbs cfv cop cplusg ctp csca cvsca cpr wcel cun cvv eqeltri
      tpex prex unex algfun mulrid opex tpid3 elun1 ax-mp eleqtrri strfv ) EAJG
      IAKLMBNZKOMCNZKJMZENZPZKQMDNZKRMFNZSZUAZUBHUSVBUOUPURUDUTVAUEUFUCABCDEFHU
      GIUHURVCAURUSTURVCTUOUPURUQEUIUJURUSVBUKULHUMUN $.
      $( [27-Nov-2014] $)

    $( The set of scalars of a constructed algebra.  (Contributed by Stefan
       O'Rear, 27-Nov-2014.) $)
    algsca $p |- ( S e. V -> S = ( Scalar ` A ) ) $=
      ( va csca cnx cbs cfv cop cplusg cmulr ctp cvsca cpr wcel cun cvv eqeltri
      tpex prex unex algfun scaid opex prid1 elun2 ax-mp eleqtrri strfv ) DAJGI
      AKLMBNZKOMCNZKPMENZQZKJMZDNZKRMFNZSZUAZUBHURVBUOUPUQUDUTVAUEUFUCABCDEFHUG
      IUHUTVCAUTVBTUTVCTUTVAUSDUIUJUTVBURUKULHUMUN $.
      $( [27-Nov-2014] $)

    $( The scalar product operation of a constructed algebra.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    algvsca $p |- ( M e. V -> M = ( vsca ` A ) ) $=
      ( va cvsca cnx cbs cfv cop cplusg cmulr ctp csca cpr wcel cun cvv eqeltri
      tpex prex unex algfun vscaid opex prid2 elun2 ax-mp eleqtrri strfv ) FAJG
      IAKLMBNZKOMCNZKPMENZQZKRMDNZKJMZFNZSZUAZUBHURVBUOUPUQUDUSVAUEUFUCABCDEFHU
      GIUHVAVCAVAVBTVAVCTUSVAUTFUIUJVAVBURUKULHUMUN $.
      $( [27-Nov-2014] $)
  $}

  ${
    $d W w s $.  $d S w s $.  $d ph w s a $.
    $( Lemma for ~ srabase through ~ sravsca . $)
    sraval $p |- ( ( W e. V /\ S C_ ( Base ` W ) ) ->
        ( ( subringAlg ` W ) ` S ) = ( { <. ( Base ` ndx ) , ( Base ` W ) >. ,
            <. ( +g ` ndx ) , ( +g ` W ) >. ,
            <. ( .r ` ndx ) , ( .r ` W ) >. } u.
          { <. ( Scalar ` ndx ) , ( W |`s S ) >. ,
            <. ( vsca ` ndx ) , ( .r ` W ) >. } ) ) $=
      ( vs vw wcel cbs cfv csra cnx cop cplusg cmulr cress cpr cun fveq2 opeq2d
      co wceq wss cpw ctp csca cvsca cmpt cvv elex pweqd tpeq123d oveq1 preq12d
      cv uneq12d mpteq12dv df-sra fvex pwex mptex fvmpt syl fveq1d elpw2 preq1d
      oveq2 uneq2d eqid tpex prex unex sylbir sylan9eq ) CBFZACGHZUAZACIHZHADVN
      UBZJGHZVNKZJLHZCLHZKZJMHZCMHZKZUCZJUDHZCDUMZNSZKZJUEHZWDKZOZPZUFZHZWFWGCA
      NSZKZWLOZPZVMAVPWOVMCUGFVPWOTCBUHECDEUMZGHZUBZVRXBKZVTXALHZKZWCXAMHZKZUCZ
      WGXAWHNSZKZWKXGKZOZPZUFWOUGIXACTZDXCXNVQWNXOXBVNXACGQZUIXOXIWFXMWMXOXDVSX
      FWBXHWEXOXBVNVRXPRXOXEWAVTXACLQRXOXGWDWCXACMQZRUJXOXKWJXLWLXOXJWIWGXACWHN
      UKRXOXGWDWKXQRULUNUOEDUPDVQWNVNCGUQZURUSUTVAVBVOAVQFWPWTTAVNXRVCDAWNWTVQW
      OWHATZWMWSWFXSWJWRWLXSWIWQWGWHACNVERVDVFWOVGWFWSVSWBWEVHWRWLVIVJUTVKVL $.
      $( [27-Nov-2014] $)

    srapart.a $e |- ( ph -> A = ( ( subringAlg ` W ) ` S ) ) $.
    srapart.s $e |- ( ph -> S C_ ( Base ` W ) ) $.
    $( Base set of a subring algebra.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    srabase $p |- ( ph -> ( Base ` W ) = ( Base ` A ) ) $=
      ( vw cbs cfv csra wceq cvv wcel cnx cop cplusg cmulr fveq2d c0 fvprc csca
      wss wa ctp cress co cvsca cpr cun sraval fvex eqid algbase ax-mp syl6reqr
      wn fveq1d fv01 syl6eq c1 df-base str0 syl6eqr eqtr4d adantr pm2.61ian syl
      ) ADHIZCDJIZIZHIZBHIACVHUBZVHVKKZFDLMZVLVMVNVLUCZVKNHIVHONPIDPIZONQIDQIZO
      UDNUAIDCUEUFZONUGIVQOUHUIZHIZVHVOVJVSHCLDUJRVHLMVHVTKDHUKVSVHVPVRVQVQLVSU
      LUMUNUOVNUPZVMVLWAVHSVKDHTWAVKSHISWAVJSHWAVJCSISWACVISDJTUQCURUSRHUTGGVAV
      BVCVDVEVFVGABVJHERVD $.
      $( [27-Nov-2014] $)

    $( Additive operation of a subring algebra.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    sraaddg $p |- ( ph -> ( +g ` W ) = ( +g ` A ) ) $=
      ( vw cplusg cfv csra cbs wceq cvv wcel cnx cop cmulr fveq2d c0 fvprc csca
      wss wa ctp cress co cvsca cpr cun sraval fvex eqid algaddg ax-mp syl6reqr
      wn fveq1d fv01 syl6eq plusgid str0 syl6eqr eqtr4d adantr pm2.61ian syl )
      ADHIZCDJIZIZHIZBHIACDKIZUBZVGVJLZFDMNZVLVMVNVLUCZVJOKIVKPOHIZVGPOQIDQIZPU
      DOUAIDCUEUFZPOUGIVQPUHUIZHIZVGVOVIVSHCMDUJRVGMNVGVTLDHUKVSVKVGVRVQVQMVSUL
      UMUNUOVNUPZVMVLWAVGSVJDHTWAVJSHISWAVISHWAVICSISWACVHSDJTUQCURUSRHVPGGUTVA
      VBVCVDVEVFABVIHERVC $.
      $( [27-Nov-2014] $)

    $( Multiplicative operation of a subring algebra.  (Contributed by Stefan
       O'Rear, 27-Nov-2014.) $)
    sramulr $p |- ( ph -> ( .r ` W ) = ( .r ` A ) ) $=
      ( vs cmulr cfv csra cbs wceq cvv wcel cnx cop cplusg fveq2d c0 fvprc csca
      wss wa ctp cress co cvsca cpr cun sraval fvex eqid algmulr ax-mp syl6reqr
      wn fveq1d fv01 syl6eq mulrid str0 syl6eqr eqtr4d adantr pm2.61ian syl ) A
      DHIZCDJIZIZHIZBHIACDKIZUBZVGVJLZFDMNZVLVMVNVLUCZVJOKIVKPOQIDQIZPOHIZVGPUD
      OUAIDCUEUFZPOUGIVGPUHUIZHIZVGVOVIVSHCMDUJRVGMNVGVTLDHUKVSVKVPVRVGVGMVSULU
      MUNUOVNUPZVMVLWAVGSVJDHTWAVJSHISWAVISHWAVICSISWACVHSDJTUQCURUSRHVQGGUTVAV
      BVCVDVEVFABVIHERVC $.
      $( [27-Nov-2014] $)

    $( The set of scalars of a subring algebra.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    srasca $p |- ( ph -> ( W |`s S ) = ( Scalar ` A ) ) $=
      ( vs cress csra cfv csca cbs wceq cvv wcel cnx cop cplusg fveq2d c0 co wa
      wss cmulr ctp cvsca cpr cun sraval ovex eqid algsca syl6reqr wn reldmress
      ax-mp ovprc1 fvprc fveq1d fv01 syl6eq scaid str0 syl6eqr eqtr4d pm2.61ian
      adantr syl ) ADCHUAZCDIJZJZKJZBKJACDLJZUCZVIVLMZFDNOZVNVOVPVNUBZVLPLJVMQP
      RJDRJZQPUDJDUDJZQUEPKJZVIQPUFJVSQUGUHZKJZVIVQVKWAKCNDUISVINOVIWBMDCHUJWAV
      MVRVIVSVSNWAUKULUPUMVPUNZVOVNWCVITVLDCHUOUQWCVLTKJTWCVKTKWCVKCTJTWCCVJTDI
      URUSCUTVASKVTGGVBVCVDVEVGVFVHABVKKESVE $.
      $( [27-Nov-2014] $)

    $( The scalar product operation of a subring algebra.  (Contributed by
       Stefan O'Rear, 27-Nov-2014.) $)
    sravsca $p |- ( ph -> ( .r ` W ) = ( vsca ` A ) ) $=
      ( vs cmulr cfv csra cvsca cbs wceq cvv wcel cnx cop cplusg fveq2d c0 csca
      wss wa ctp cress co cpr cun sraval fvex eqid algvsca ax-mp syl6reqr fvprc
      wn fveq1d fv01 syl6eq vscaid str0 syl6eqr eqtr4d adantr pm2.61ian syl ) A
      DHIZCDJIZIZKIZBKIACDLIZUBZVGVJMZFDNOZVLVMVNVLUCZVJPLIVKQPRIDRIZQPHIVGQUDP
      UAIDCUEUFZQPKIZVGQUGUHZKIZVGVOVIVSKCNDUISVGNOVGVTMDHUJVSVKVPVQVGVGNVSUKUL
      UMUNVNUPZVMVLWAVGTVJDHUOWAVJTKITWAVITKWAVICTITWACVHTDJUOUQCURUSSKVRGGUTVA
      VBVCVDVEVFABVIKESVC $.
      $( [27-Nov-2014] $)
  $}

  ${
    $d x y z S $.  $d x y A $.  $d x y z W $.
    sralmod.a $e |- A = ( ( subringAlg ` W ) ` S ) $.
    $( The subring algebra is a left module.  (Contributed by Stefan O'Rear,
       27-Nov-2014.) $)
    sralmod $p |- ( S e. ( SubRing ` W ) -> A e. LMod ) $=
      ( vx vy vz cfv wcel cplusg co wceq a1i eqid cv adantr w3a sseldi syl13anc
      wa csubrg cbs cin cmulr cress csra subrgss srabase sraaddg srasca sravsca
      cur ressbas ressplusg ressmulr subrg1 subrgrng cgrp subrgrcl rnggrp eqidd
      crg oveqd grppropd mpbid 3ad2ant1 inss2 sseli 3ad2ant2 simp3 rngcl simpr1
      syl syl3anc simpr2 simpr3 rngdi rngdir rngass rnglidm sylan islmodd ) BCU
      AHZIZEFGCJHZBCUBHZUCZWECUDHZWHCULHZCBUEKZWFAWDABCABCUFHHLWDDMZBWFCWFNZUGZ
      UHZWDABCWKWMUIZWDABCWKWMUJWDABCWKWMUKBWFWJWCCWJNZWLUMWEWJJHLWDBWECWJWPWEN
      ZUNMWHWJUDHLWDBCWJWHWPWHNZUOMBCWJWIWPWINZUPBCWJWPUQWDCURIZAURIWDCVBIZWTBC
      USZCUTVMWDEFWFCAWDWFVAWNWDEOZFOZWEKZXCXDAJHZKLXCWFIZXDWFIZTWDWEXFXCXDWOVC
      PVDVEWDXCWGIZXHQXAXGXHXCXDWHKZWFIWDXIXAXHXBVFXIWDXGXHWGWFXCBWFVGZVHVIWDXI
      XHVJWFCWHXCXDWLWRVKVNWDXIXHGOZWFIZQZTZXAXGXHXMXCXDXLWEKWHKXJXCXLWHKZWEKLW
      DXAXNXBPXOWGWFXCXKWDXIXHXMVLRWDXIXHXMVOWDXIXHXMVPWFWECWHXCXDXLWLWQWRVQSWD
      XIXDWGIZXMQZTZXAXGXHXMXEXLWHKXPXDXLWHKZWEKLWDXAXRXBPZXSWGWFXCXKWDXIXQXMVL
      RZXSWGWFXDXKWDXIXQXMVORZWDXIXQXMVPZWFWECWHXCXDXLWLWQWRVRSXSXAXGXHXMXJXLWH
      KXCXTWHKLYAYBYCYDWFCWHXCXDXLWLWRVSSWDXAXGWIXCWHKXCLXBWFCWHWIXCWLWRWSVTWAW
      B $.
      $( [27-Nov-2014] $)

    $(
    @( The subring algebra is a left vector space. @)
    sralvec @p |- ( ( W e. Ring /\ S e. ( SubRing ` W ) /\
        ( W |`s S ) e. DivRing ) -> A e. LVec ) @=
      ? @.
    $)
  $}

  ${
    $d a b A $.  $d a b ph $.  $d a b W $.
    sralmod0.a $e |- ( ph -> A = ( ( subringAlg ` W ) ` S ) ) $.
    sralmod0.z $e |- ( ph -> Z = ( 0g ` W ) ) $.
    sralmod0.s $e |- ( ph -> S C_ ( Base ` W ) ) $.
    sralmod0.w $e |- ( ph -> W e. V ) $.
    $( The subring module inherits a zero from its ring. $)
    sralmod0 $p |- ( ph -> Z = ( 0g ` A ) ) $=
      ( va vb c0g cfv cbs cv cplusg co wcel cvv eqidd srabase wa sraaddg adantr
      wceq oveqd elex syl csra fvex syl6eqel grpidpropd eqtrd ) AFEMNBMNHAKLEON
      ZEBAUOUAABCEGIUBAKPZLPZEQNZRUPUQBQNZRUFUPUOSUQUOSUCAURUSUPUQABCEGIUDUGUEA
      EDSETSJEDUHUIABCEUJNZNTGCUTUKULUMUN $.
      $( [27-Dec-2014] $) $( [6-Dec-2014] $)
  $}

  ${
    $d ph x y z $.  $d Z x y z $.  $d P x y z $.  $d T x y z $.  $d O x y z $.
    $d S x y z $.  $d I x y z $.  $d N x y z $.  $d D x y z $.
    issubgrpd.s $e |- ( ph -> S = ( I |`s D ) ) $.
    issubgrpd.z $e |- ( ph -> Z = ( 0g ` I ) ) $.
    issubgrpd.p $e |- ( ph -> P = ( +g ` I ) ) $.
    issubgrpd.ss $e |- ( ph -> D C_ ( Base ` I ) ) $.
    issubgrpd.zcl $e |- ( ph -> Z e. D ) $.
    issubgrpd.acl $e |- ( ( ph /\ x e. D /\ y e. D ) -> ( x P y ) e. D ) $.
    issubgrpd.ncl $e |- ( ( ph /\ x e. D ) -> ( ( invg ` I ) ` x ) e. D ) $.

    ${
      issubgrpd.g $e |- ( ph -> I e. Grp ) $.
      $( Prove a subgroup by closure (definition version). $)
      issubgrpd2 $p |- ( ph -> D e. ( SubGrp ` I ) ) $=
        ( cfv wcel wa eqid csubg cbs wss c0 wne cv cplusg wral cminusg cgrp w3a
        co wb c0g issubg2 syl ne0i wceq oveqd ad2antrr 3expa eqeltrrd ralrimiva
        jca mpbir3and ) ADGUAQRZDGUBQZUCZDUDUEZBUFZCUFZGUGQZULZDRZCDUHZVJGUIQZQ
        DRZSZBDUHZAGUJRVFVHVIVSUKUMPBCVGVLDGVPGUNQZVGTVTTVLTVPTUOUPLAHDRVIMDHUQ
        UPAVRBDAVJDRZSZVOVQWBVNCDWBVKDRZSVJVKEULZVMDAWDVMURWAWCAEVLVJVKKUSUTAWA
        WCWDDRNVAVBVCOVDVCVE $.
        $( [7-Dec-2014] $)

      $( Prove a subgroup by closure. $)
      issubgrpd $p |- ( ph -> S e. Grp ) $=
        ( cress co cgrp wcel csubg cfv issubgrpd2 eqid subggrp syl eqeltrd ) AF
        GDQRZSIADGUAUBTUHSTABCDEFGHIJKLMNOPUCDGUHUHUDUEUFUG $.
        $( [7-Dec-2014] $) $( [29-Nov-2014] $)
    $}

    ${
      issubabld.g $e |- ( ph -> I e. Abel ) $.
      $( Prove an Abelian subgroup by closure. $)
      issubabld $p |- ( ph -> S e. Abel ) $=
        ( cress co cabel wcel csubg cfv cgrp ablgrp syl issubgrpd2 eqid subgabl
        syl2anc eqeltrd ) AFGDQRZSIAGSTZDGUAUBTUKSTPABCDEFGHIJKLMNOAULGUCTPGUDU
        EUFDGUKUKUGUHUIUJ $.
        $( [7-Dec-2014] $) $( [30-Nov-2014] $)
    $}

    ${
      issubrngd.o $e |- ( ph -> O = ( 1r ` I ) ) $.
      issubrngd.t $e |- ( ph -> T = ( .r ` I ) ) $.
      issubrngd.ocl $e |- ( ph -> O e. D ) $.
      issubrngd.tcl $e |- ( ( ph /\ x e. D /\ y e. D ) -> ( x T y ) e. D ) $.

      ${
        issubrngd.g $e |- ( ph -> I e. Ring ) $.
        $( Prove a subring by closure (definition version). $)
        issubrngd2 $p |- ( ph -> D e. ( SubRing ` I ) ) $=
          ( csubrg cfv wcel csubg cur cv cmulr co wral crg w3a wb eqid issubrg2
          cbs syl cgrp rnggrp issubgrpd2 eqeltrrd oveqd adantr 3expb ralrimivva
          wa wceq mpbir3and ) ADHUCUDUEZDHUFUDUEZHUGUDZDUEZBUHZCUHZHUIUDZUJZDUE
          ZCDUKBDUKZAHULUEZVJVKVMVSUMUNUBBCDHUQUDZHVPVLWAUOVLUOVPUOUPURABCDEFHJ
          KLMNOPQAVTHUSUEUBHUTURVAAIVLDRTVBAVRBCDDAVNDUEZVODUEZVGZVGVNVOGUJZVQD
          AWEVQVHWDAGVPVNVOSVCVDAWBWCWEDUEUAVEVBVFVI $.
          $( [7-Dec-2014] $) $( [30-Nov-2014] $)

        $( Prove a subring by closure. $)
        issubrngd $p |- ( ph -> S e. Ring ) $=
          ( cress co crg csubrg cfv wcel issubrngd2 eqid subrgrng syl eqeltrd )
          AFHDUCUDZUEKADHUFUGUHUNUEUHABCDEFGHIJKLMNOPQRSTUAUBUIDHUNUNUJUKULUM
          $.
          $( [7-Dec-2014] $) $( [30-Nov-2014] $)
      $}

      issubdrngd.rcl $e |- ( ( ph /\ ( x e. D /\ x =/= Z ) ) ->
          ( ( invr ` I ) ` x ) e. D ) $.

      ${
        issubdrngd.g $e |- ( ph -> I e. DivRing ) $.
        $( Prove a sub-division-ring by closure. $)
        issubdrngd $p |- ( ph -> S e. DivRing ) $=
          ( cress co cdr wcel cv cinvr cfv c0g csn cdif wral wne neeq2d eldifsn
          anbi2d syl6bbr biimpar syldan ralrimiva csubrg crg drngrng issubrngd2
          wa wb syl eqid issubdrg syl2anc mpbird eqeltrd ) AFHDUDUEZUFKAVOUFUGZ
          BUHZHUIUJZUJDUGZBDHUKUJZULUMZUNZAVSBWAAVQWAUGZVQDUGZVQJUOZVGZVSAWFWCA
          WFWDVQVTUOZVGWCAWEWGWDAJVTVQLUPURVQDVTUQUSUTUBVAVBAHUFUGZDHVCUJUGVPWB
          VHUCABCDEFGHIJKLMNOPQRSTUAAWHHVDUGUCHVEVIVFBDHVOVRVTVOVJVTVJVRVJVKVLV
          MVN $.
          $( [7-Dec-2014] $) $( [30-Nov-2014] $)
      $}
    $}
  $}

  ${
    grpcld.r $e |- ( ph -> G e. Grp ) $.
    grpcld.b $e |- ( ph -> B = ( Base ` G ) ) $.

    ${
      grpcld.z $e |- ( ph -> Z = ( 0g ` G ) ) $.
      $( Deduction version of ~ grpidcl . $)
      grpidcld $p |- ( ph -> Z e. B ) $=
        ( wcel c0g cfv cbs cgrp eqid grpidcl syl eleq12d mpbird ) ADBHCIJZCKJZH
        ZACLHTESCRSMRMNOADRBSGFPQ $.
        $( [30-Nov-2014] $)
    $}

    ${
      grpinvcld.n $e |- ( ph -> N = ( invg ` G ) ) $.
      grpinvcld.x $e |- ( ph -> X e. B ) $.
      $( Deduction verison of ~ grpinvcl . $)
      grpinvcld $p |- ( ph -> ( N ` X ) e. B ) $=
        ( cfv wcel cminusg cgrp eleqtrd eqid grpinvcl syl2anc fveq1d eleq12d
        cbs mpbird ) AEDJZBKECLJZJZCTJZKZACMKEUEKUFFAEBUEIGNUECUCEUEOUCOPQAUBUD
        BUEAEDUCHRGSUA $.
        $( [30-Nov-2014] $)
    $}

    grpcld.p $e |- ( ph -> P = ( +g ` G ) ) $.
    grpcld.x $e |- ( ph -> X e. B ) $.
    grpcld.y $e |- ( ph -> Y e. B ) $.
    $( Deduction version of ~ grpcl . $)
    grpcld $p |- ( ph -> ( X P Y ) e. B ) $=
      ( co wcel cplusg cfv cbs cgrp eleqtrd eqid grpcl syl3anc eleq12d mpbird
      oveqd ) AEFCLZBMEFDNOZLZDPOZMZADQMEUHMFUHMUIGAEBUHJHRAFBUHKHRUHUFDEFUHSUF
      STUAAUEUGBUHACUFEFIUDHUBUC $.
      $( [30-Nov-2014] $)
  $}

  ${
    rngcld.r $e |- ( ph -> R e. Ring ) $.
    rngcld.b $e |- ( ph -> B = ( Base ` R ) ) $.

    ${
      rngcld.o $e |- ( ph -> O = ( 1r ` R ) ) $.
      $( Deduction version of ~ rngidcl . $)
      rngidcld $p |- ( ph -> O e. B ) $=
        ( wcel cur cfv cbs crg eqid rngidcl syl eleq12d mpbird ) ADBHCIJZCKJZHZ
        ACLHTESCRSMRMNOADRBSGFPQ $.
        $( [30-Nov-2014] $)
    $}

    rngcld.t $e |- ( ph -> T = ( .r ` R ) ) $.
    rngcld.x $e |- ( ph -> X e. B ) $.
    rngcld.y $e |- ( ph -> Y e. B ) $.
    $( Deduction version of ~ rngcl . $)
    rngcld $p |- ( ph -> ( X T Y ) e. B ) $=
      ( co wcel cmulr cfv cbs crg eleqtrd eqid rngcl syl3anc eleq12d mpbird
      oveqd ) AEFDLZBMEFCNOZLZCPOZMZACQMEUHMFUHMUIGAEBUHJHRAFBUHKHRUHCUFEFUHSUF
      STUAAUEUGBUHADUFEFIUDHUBUC $.
      $( [30-Nov-2014] $)
  $}

  ${
    $d x y z $.

    $(
    @( The rationals are a sub-division-ring of ` CCfld ` . @)
    cnqdrng @p |- ( CCfld |`s QQ ) e. DivRing @=
      ( vx vy ccnfld cq co cdr wcel wtru caddc cmul c1 cc0 cfv wceq cc cz ax-mp
      a1i zq cv cress eqidd c0g cnfld0 cplusg cnfldadd cbs wss cnfldbas sseqtri
      0z qaddcl 3adant1 cminusg cneg qcn cnfldneg syl qnegcl eqeltrd adantl cur
      qsscn cnfld1 cmulr cnfldmul 1z qmulcl wa cinvr cdiv cnfldrec sylan qreccl
      wne cndrng issubdrngd trud ) CDUAEZFGHABDIVSJCKLHVSUBLCUCMNHUDRICUEMNHUFR
      DCUGMZUHHDOVTVCUIUJRLDGZHLPGWAUKLSQRATZDGZBTZDGZWBWDIEDGHWBWDULUMWCWBCUNM
      MZDGHWCWFWBUOZDWCWBOGZWFWGNWBUPZWBUQURWBUSUTVAKCVBMNHVDRJCVEMNHVFRKDGZHKP
      GWJVGKSQRWCWEWBWDJEDGHWBWDVHUMWCWBLVOZVIZWBCVJMMZDGHWLWMKWBVKEZDWCWHWKWMW
      NNWIWBVLVMWBVNUTVACFGHVPRVQVR @.
      @( [30-Nov-2014] @)
    $)

    $( The integers are a subring of ` CCfld ` . $)
    cnzring $p |- ( CCfld |`s ZZ ) e. Ring $=
      ( vx vy ccnfld cz cress co crg wcel wtru caddc cmul c1 cc0 eqidd cfv wceq
      a1i cc cv 3adant1 c0g cnfld0 cplusg cnfldadd cbs wss zsscn sseqtri zaddcl
      cnfldbas 0z cminusg cneg zcn cnfldneg syl znegcl eqeltrd adantl cur cmulr
      cnfld1 cnfldmul 1z zmulcl cnrng issubrngd trud ) CDEFZGHIABDJVIKCLMIVINMC
      UAOPIUBQJCUCOPIUDQDCUEOZUFIDRVJUGUJUHQMDHIUKQASZDHZBSZDHZVKVMJFDHIVKVMUIT
      VLVKCULOOZDHIVLVOVKUMZDVLVKRHVOVPPVKUNVKUOUPVKUQURUSLCUTOPIVBQKCVAOPIVCQL
      DHIVDQVLVNVKVMKFDHIVKVMVETCGHIVFQVGVH $.
      $( [30-Nov-2014] $)
  $}

