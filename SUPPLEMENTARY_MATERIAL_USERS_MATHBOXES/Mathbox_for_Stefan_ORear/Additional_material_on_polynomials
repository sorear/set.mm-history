$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Additional material on polynomials
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Monic Poly< $.

  $( Extend class notation with the class of monic polynomials. $)
  cmnc $a class Monic $.

  $( Extend class notatin with the class of limited-degree polynomials. $)
  cplylt $a class Poly< $.

  ${
    $d s p x $.

    $( Define the class of monic polynomials. $)
    df-mnc $a |- Monic = ( s e. ~P CC |-> { p e. ( Poly ` s ) |
        ( ( coeff ` p ) ` ( deg ` p ) ) = 1 } ) $.

    $( Define the class of limited-degree polynomials. $)
    df-plylt $a |- Poly< = ( s e. ~P CC , x e. NN0 |-> { p e. ( Poly ` s ) |
        ( p = 0p \/ ( deg ` p ) < x ) } ) $.
  $}

  ${
    dgrsub2.a $e |- N = ( deg ` F ) $.
    $( Subtracting two polynomials with the same degree and top coefficient
       gives a polynomial of strictly lower degree.  (Contributed by Stefan
       O'Rear, 25-Nov-2014.) $)
    dgrsub2 $p |- ( ( ( F e. ( Poly ` S ) /\ G e. ( Poly ` T ) ) /\
          ( ( deg ` G ) = N /\ N e. NN /\
            ( ( coeff ` F ) ` N ) = ( ( coeff ` G ) ` N ) ) ) ->
        ( deg ` ( F oF - G ) ) < N ) $=
      ( cply cfv wcel wa cdgr wceq clt wbr cc0 syl cle cc cn0 eqid cn ccoe cmin
      w3a cof co c0p wi simpr2 nngt0 syl5eqbr fveq2 breq1d syl5ibrcom wo plyssc
      dgr0 wb sseli plysubcl syl2an adantr nnnn0 dgrlt cif dgrsub simpr1 eqcomi
      syl2anc a1i ifeq12 ifid syl6eq breqtrd coesub fveq1d cvv wfn simpll coef3
      wf 3syl simplr nn0ex inidm simplr3 eqidd ofval mpdan ffvelrn subid 3eqtrd
      ffn mpbir2and ord pm2.61d ) CAGHZIZDBGHZIZJZDKHZELZEUAIZECUBHZHEDUBHZHZLZ
      UDZJZCDUCUEZUFZUGLZXLKHZEMNZXJXDXMXOUHXAXCXDXHUIZXDXOXMUGKHZEMNXDXQOEMUQE
      UJUKXMXNXQEMXLUGKULUMUNPXJXMXOXJXMXOUOZXNEQNZEXLUBHZHZOLZXJXLRGHZIZESIZXR
      XSYBJURXAYDXIWRCYCIZDYCIZYDWTWQYCCAUPUSZWSYCDBUPUSZRCDUTVAVBXJXDYEXPEVCPZ
      XTRXLEXNXNTXTTVDVIXJXNCKHZXBQNZXBYKVEZEQXAXNYMQNZXIWRYFYGYNWTYHYIRCDYKXBY
      KTXBTVFVAVBXJYMYLEEVEZEXJXCYKELZYMYOLXAXCXDXHVGYPXJEYKFVHVJYLXBEYKEVKVIYL
      EVLVMVNXJYAEXEXFXKUFZHZXGXGUCUFZOXJEXTYQXAXTYQLZXIWRYFYGYTWTYHYIXEXFRCDXE
      TZXFTZVOVAVBVPXJYEYRYSLYJXJSSXGXGUCSXEXFVQVQEXJWRSRXEWAXESVRWRWTXIVSXEACU
      UAVTSRXEWMWBXJWTSRXFWAZXFSVRWRWTXIWCZXFBDUUBVTZSRXFWMWBSVQIXJWDVJZUUFSWEX
      CXDXHXAYEWFXJYEJXGWGWHWIXJXGRIZYSOLXJUUCYEUUGXJWTUUCUUDUUEPYJSREXFWJVIXGW
      KPWLWNWOWP $.
      $( [25-Nov-2014] $)
  $}

  ${
    $( A nonzero polynomial with a root has positive degree.  TODO: use in
       ~ aaliou2 .  (Contributed by Stefan O'Rear, 25-Nov-2014.) $)
    dgrnznn $p |- ( ( ( P e. ( Poly ` S ) /\ P =/= 0p ) /\
        ( A e. CC /\ ( P ` A ) = 0 ) ) -> ( deg ` P ) e. NN ) $=
      ( cply cfv wcel c0p wne wa cc cc0 wceq cdgr wn cn csn cxp simpr ad2antrr
      wo fveq1d simplr fvex fvconst2 3eqtr3rd sneqd xpeq2d eqtrd df-0p necon3ad
      syl6eqr ex impcom adantll 0dgrb mtbird cn0 dgrcl elnn0 sylib orel2 sylc
      wb ) BCDEFZBGHZIAJFZABEZKLZIZIZBMEZKLZNVKOFZVLTZVMVJVLBJKBEZPZQZLZVEVIVRN
      ZVDVIVEVSVIVRBGVIVRBGLVIVRIZBJKPZQZGVTBVQWBVIVRRZVTVPWAJVTVOKVTVGAVQEZKVO
      VTABVQWCUAVFVHVRUBVFWDVOLVHVRJVOAKBUCUDSUEUFUGUHUIUKULUJUMUNVDVLVRVCVEVIC
      BUOSUPVJVKUQFZVNVDWEVEVICBURSVKUSUTVLVMVAVB $.
      $( [25-Nov-2014] $)
  $}

  ${
    $d S s p $.  $d P s p $.

    $( Property of a monic polynomial.  (Contributed by Stefan O'Rear,
       5-Dec-2014.) $)
    elmnc $p |- ( P e. ( Monic ` S ) <-> ( P e. ( Poly ` S ) /\
          ( ( coeff ` P ) ` ( deg ` P ) ) = 1 ) ) $=
      ( vs vp cmnc cfv wcel cc wss cply cdgr ccoe c1 wceq wa cdm cpw crab fveq2
      cv elfvdm df-mnc sseli elpwi 3syl plybss adantr cnex elpw2 rabeq syl fvex
      dmmptss rabex fvmpt sylbir eleq2d fveq12d eqeq1d elrab syl6bb pm5.21nii )
      ABEFZGZBHIZABJFZGZAKFZALFZFZMNZOZVDBEPZGBHQZGZVEABEUAVMVNBCVNDTZKFZVPLFZF
      ZMNZDCTZJFZRZECDUBZUMUCBHUDUEVGVEVKBAUFUGVEVDAVTDVFRZGVLVEVCWEAVEVOVCWENB
      HUHUICBWCWEVNEWABNWBVFNWCWENWABJSVTDWBVFUJUKWDVTDVFBJULUNUOUPUQVTVKDAVFVP
      ANZVSVJMWFVQVHVRVIVPALSVPAKSURUSUTVAVB $.
      $( [5-Dec-2014] $)

    $( A monic polynomial is a polynomial.  (Contributed by Stefan O'Rear,
       5-Dec-2014.) $)
    mncply $p |- ( P e. ( Monic ` S ) -> P e. ( Poly ` S ) ) $=
      ( cmnc cfv wcel cply cdgr ccoe c1 wceq elmnc simplbi ) ABCDEABFDEAGDAHDDI
      JABKL $.
      $( [5-Dec-2014] $)

    $( A monic polynomial has leading coefficient 1.  (Contributed by Stefan
       O'Rear, 5-Dec-2014.) $)
    mnccoe $p |- ( P e. ( Monic ` S ) -> ( ( coeff ` P ) ` ( deg ` P ) ) = 1 )
        $=
      ( cmnc cfv wcel cply cdgr ccoe c1 wceq elmnc simprbi ) ABCDEABFDEAGDAHDDI
      JABKL $.
      $( [5-Dec-2014] $)

    $( A monic polynomial is not zero.  (Contributed by Stefan O'Rear,
       5-Dec-2014.) $)
    mncn0 $p |- ( P e. ( Monic ` S ) -> P =/= 0p ) $=
      ( cmnc cfv wcel cdgr ccoe wceq c0p wne mnccoe cc0 cn0 csn cxp coe0 fveq1i
      c1 0nn0 fveq2 eqeltri elexi fvconst2 ax-mp ax-1ne0 necomi eqnetri fveq12d
      dgr0 eqtri neeq1d mpbiri necon2i syl ) ABCDEAFDZAGDZDZRHAIJABKAIUQRAIHZUQ
      RJIFDZIGDZDZRJVALRVAUSMLNOZDZLUSUTVBPQUSMEVCLHUSLMUISUAMLUSLMSUBUCUDUJRLU
      EUFUGURUQVARURUOUSUPUTAIGTAIFTUHUKULUMUN $.
      $( [5-Dec-2014] $)
  $}

  $(
  @{
    mncdiv.s @e |- ( ph -> S e. ( SubRing ` CCfld ) ) @.
    mncdiv.f @e |- ( ph -> F e. ( Poly ` S ) ) @.
    mncdiv.g @e |- ( ph -> G e. ( Monic ` S ) ) @.
    @( Monic version of polynomial division algorithm, does not require
       division over the base ring. @)
    mncdivex @p |- ( ph -> E. q e. ( Poly ` S ) ( F oF - ( G oF x. q ) ) e.
          ( S Poly< ( deg ` G ) ) ) @=
      ? @.
  @}
  $)

