$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Moore closures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F c x s $.  $d C c x s $.  $d X c x s $.  $d U c x s $.  $d V c x s $.
    $( The domain and range of the function expression for Moore closures. $)
    mrcflem $p |- ( C e. ( Moore ` X ) ->
        ( x e. ~P X |-> |^| { s e. C | x C_ s } ) : ~P X --> C ) $=
      ( cmre cfv wcel cpw cv wss crab cint cmpt wa wne simpl ssrab2 a1i mre1cl
      c0 adantr elpwi sseq2 elrab sylanbrc ne0i syl mreintcl syl3anc eqid fmptd
      adantl ) BCEFGZACHZAIZDIZJZDBKZLZBAUNUSMZUMUOUNGZNZUMURBJZURTOZUSBGUMVAPV
      CVBUQDBQRVBCURGZVDVBCBGZUOCJZVEUMVFVABCSUAVAVGUMUOCUBULUQVGDCBUPCUOUCUDUE
      URCUFUGBURCUHUIUTUJUK $.
      $( [31-Jan-2015] $)

    $( Moore-closure is a well behaved function.  (Contributed by Stefan
       O'Rear, 1-Feb-2015.) $)
    fnmrc $p |- mrCls Fn U. ran Moore $=
      ( vx vc vs cv cuni cpw wss crab cint cmpt cvv wcel cmrc cmre df-mrc fnmpt
      crn wfn cfv mreunirn cxp wf mrcflem fssxp syl vex uniex pwex xpex sylancl
      ssexg sylbi mprg ) ABDZEZFZADCDGCUNHIJZKLZMNQEZRBUSBUSUQMKACBOPUNUSLUNUON
      SLZURUNTUTUQUPUNUAZGZVAKLURUTUPUNUQUBVBAUNUOCUCUPUNUQUDUEUPUNUOUNBUFZUGUH
      VCUIUQVAKUKUJULUM $.
      $( [1-Feb-2015] $)

    mrcfval.f $e |- F = ( mrCls ` C ) $.
    $( Value of the function expression for the Moore closure.  (Contributed by
       Stefan O'Rear, 31-Jan-2015.) $)
    mrcfval $p |- ( C e. ( Moore ` X ) -> F =
        ( x e. ~P X |-> |^| { s e. C | x C_ s } ) ) $=
      ( vc cmre cfv wcel cmrc cpw cv wss crab cint cmpt cuni wceq cvv fvssunirn
      crn sseli unieq pweqd rabeq inteqd mpteq12dv df-mrc cxp wf mreunirn sylbi
      mrcflem fssxp syl uniex pwex xpex ssexg sylancl fvmpt3 mreuni eqidd eqtrd
      vex syl5eq ) BDHIZJZCBKIZADLZAMEMNZEBOZPZQZFVIVJABRZLZVNQZVOVIBHUBRZJVJVR
      SVHVSBHDUAUCGBAGMZRZLZVLEVTOZPZQZVRVSKTVTBSZAWBWDVQVNWFWAVPVTBUDUEWFWCVMV
      LEVTBUFUGUHAEGUIVTVSJZWEWBVTUJZNZWHTJWETJWGWBVTWEUKZWIWGVTWAHIJWJVTULAVTW
      AEUNUMWBVTWEUOUPWBVTWAVTGVFZUQURWKUSWEWHTUTVAVBUPVIAVQVNVKVNVIVPDBDVCUEVI
      VNVDUHVEVG $.
      $( [31-Jan-2015] $)

    $( The Moore closure is a function mapping arbitrary subsets to closed
       sets.  (Contributed by Stefan O'Rear, 31-Jan-2015.) $)
    mrcf $p |- ( C e. ( Moore ` X ) -> F : ~P X --> C ) $=
      ( vx vs cmre cfv wcel cpw wf cv wss crab cint cmpt mrcflem wceq mrcfval
      wb feq1 syl mpbird ) ACGHIZCJZABKZUEAEUEELFLMFANOPZKZEACFQUDBUGRUFUHTEABC
      FDSUEABUGUAUBUC $.
      $( [31-Jan-2015] $)

    $( Evaluation of the Moore closure of a set.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrcval $p |- ( ( C e. ( Moore ` X ) /\ U C_ X ) -> ( F ` U ) =
        |^| { s e. C | U C_ s } ) $=
      ( vx cmre cfv wcel wss wa cpw cv crab cint wceq wb syl adantr biimpar wne
      mre1cl elpw2g c0 simpl ssrab2 a1i simpr sseq2 elrab sylanbrc ne0i syl3anc
      mreintcl ax-17 sseq1 rabbidv inteqd adantl cmpt mrcfval fvmptd mpd3an23 )
      ADHIJZBDKZLZBDMZJZBENZKZEAOZPZAJZBCIVMQVEVIVFVEDAJZVIVFRADUCZBDAUDSUAVGVE
      VLAKZVLUEUBZVNVEVFUFVQVGVKEAUGUHVGDVLJZVRVGVOVFVSVEVOVFVPTVEVFUIVKVFEDAVJ
      DBUJUKULVLDUMSAVLDUOUNVGGBGNZVJKZEAOZPZVMVHCAVGGUPVTBQZWCVMQVGWDWBVLWDWAV
      KEAVTBVJUQURUSUTVECGVHWCVAQVFGACDEFVBTVCVD $.
      $( [31-Jan-2015] $)

    $( The Moore closure of a set is a closed set.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrccl $p |- ( ( C e. ( Moore ` X ) /\ U C_ X ) -> ( F ` U ) e. C ) $=
      ( cmre cfv wcel wss wa cpw wf adantr wb mre1cl elpw2g syl biimpar ffvelrn
      mrcf syl2anc ) ADFGHZBDIZJDKZACLZBUDHZBCGAHUBUEUCACDETMUBUFUCUBDAHUFUCNAD
      OBDAPQRUDABCSUA $.
      $( [31-Jan-2015] $)

    $( The Moore closure of a singleton is a closed set.  (Contributed by
       Stefan O'Rear, 31-Jan-2015.) $)
    mrcsncl $p |- ( ( C e. ( Moore ` X ) /\ U e. X ) -> ( F ` { U } ) e. C ) $=
      ( wcel cmre cfv csn wss snssi mrccl sylan2 ) BDFADGHFBIZDJNCHAFBDKANCDELM
      $.
      $( [31-Jan-2015] $)

    $( The closure of a closed set is itself.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrcid $p |- ( ( C e. ( Moore ` X ) /\ U e. C ) -> ( F ` U ) = U ) $=
      ( vs cmre cfv wcel wa cv crab cint wceq mress mrcval syldan intmin adantl
      wss eqtrd ) ADGHIZBAIZJBCHZBFKTFALMZBUBUCBDTUDUENABDOABCDFEPQUCUEBNUBFBAR
      SUA $.
      $( [31-Jan-2015] $)

    $( The closure of a set is a subset of the base.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrcssv $p |- ( C e. ( Moore ` X ) -> ( F ` U ) C_ X ) $=
      ( cmre cfv wcel crn cuni fvssunirn cpw wf wss mrcf frn uniss 3syl sseqtrd
      mreuni syl5ss ) ADFGHZBCGCIZJZDCBKUBUDAJZDUBDLZACMUCANUDUENACDEOUFACPUCAQ
      RADTSUA $.
      $( [31-Jan-2015] $)

    $( A set is closed iff it is equal to its closure.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrcidb $p |- ( C e. ( Moore ` X ) -> ( U e. C <-> ( F ` U ) = U ) ) $=
      ( cmre cfv wcel wceq mrcid wa simpr mrcssv adantr eqsstr3d mrccl eqeltrrd
      wss syldan impbida ) ADFGHZBAHBCGZBIZABCDEJUAUCKZUBBAUAUCLZUAUCBDRUBAHUDB
      UBDUEUAUBDRUCABCDEMNOABCDEPSQT $.
      $( [31-Jan-2015] $)

    $( Closure preserves subset ordering.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrcss $p |- ( ( C e. ( Moore ` X ) /\ U C_ V /\ V C_ X ) ->
        ( F ` U ) C_ ( F ` V ) ) $=
      ( vs cmre cfv wcel wss w3a cv crab cint wi sstr2 adantr wceq mrcval intss
      ss2rabdv syl 3ad2ant2 simp1 sstr 3adant1 syl2anc 3adant2 3sstr4d ) AEHIJZ
      BDKZDEKZLZBGMZKZGANZOZDUOKZGANZOZBCIZDCIZULUKURVAKZUMULUTUQKVDULUSUPGAULU
      SUPPUOAJBDUOQRUBUTUQUAUCUDUNUKBEKZVBURSUKULUMUEULUMVEUKBDEUFUGABCEGFTUHUK
      UMVCVASULADCEGFTUIUJ $.
      $( [31-Jan-2015] $)

    $( The closure of a set is a superset.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrcssid $p |- ( ( C e. ( Moore ` X ) /\ U C_ X ) -> U C_ ( F ` U ) ) $=
      ( vs cmre cfv wcel wss wa cv crab cint ssintub mrcval syl5sseqr ) ADGHIBD
      JKBFLJFAMNBBCHFBAOABCDFEPQ $.
      $( [31-Jan-2015] $)

    $( The closure operation is idempotent.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrcidm $p |- ( ( C e. ( Moore ` X ) /\ U C_ X ) ->
        ( F ` ( F ` U ) ) = ( F ` U ) ) $=
      ( cmre cfv wcel wss wceq mrccl mrcid syldan ) ADFGHBDIBCGZAHNCGNJABCDEKAN
      CDELM $.
      $( [31-Jan-2015] $)

    $( The closure is the minimal closed set; any closed set which contains the
       generators is a superset of the closure.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrcsscl $p |- ( ( C e. ( Moore ` X ) /\ U C_ V /\ V e. C ) ->
        ( F ` U ) C_ V ) $=
      ( cmre cfv wcel wss w3a mress 3adant2 mrcss syld3an3 wceq mrcid sseqtrd )
      AEGHIZBDJZDAIZKBCHZDCHZDSTUADEJZUBUCJSUAUDTADELMABCDEFNOSUAUCDPTADCEFQMR
      $.
      $( [31-Jan-2015] $)

    $( Idempotence of closure under a general union.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrcuni $p |- ( ( C e. ( Moore ` X ) /\ U C_ ~P X ) ->
        ( F ` U. U ) = ( F ` U. ( F " U ) ) ) $=
      ( vs vx cfv wcel wss wa cuni wral syl syl2anc adantr unissb mrcss syl3anc
      sylibr cmre cpw cima simpl cv simpll ssel2 elpwi adantll mrcssid wfun cdm
      wi wf mrcf ffun wceq fdm sseq2d biimpar funfvima2 elssuni sstrd ralrimiva
      imp mrcssv ralrimivw wfn wb ffn sseq1 ralima mpbird adantl sspwuni biimpi
      sylan mrcidm sseqtrd eqssd ) ADUAHIZBDUBZJZKZBLZCHZCBUCZLZCHZWDWAWEWHJZWH
      DJZWFWIJWAWCUDZWDFUEZWHJZFBMWJWDWNFBWDWMBIZKZWMWMCHZWHWPWAWMDJZWMWQJWAWCW
      OUFWCWOWRWAWCWOKWMWBIWRBWBWMUGWMDUHNUIAWMCDEUJOWPWQWGIZWQWHJWDWOWSWDCUKZB
      CULZJZWOWSUMWAWTWCWAWBACUNZWTACDEUOZWBACUPNPWAXBWCWAXAWBBWAXCXAWBUQXDWBAC
      URNUSUTBWMCVAOVEWQWGVBNVCVDFBWHQTWDWRFWGMZWKWDXEGUEZCHZDJZGBMZWDXHGBWAXHW
      CAXFCDEVFPVGWACWBVHZWCXEXIVIWAXCXJXDWBACVJNZWRXHFGWBBCWMXGDVKVLVQVMFWGDQT
      AWECWHDERSWDWIWFCHZWFWDWAWHWFJZWFDJZWIXLJWLWDWMWFJZFWGMZXMWDXPXGWFJZGBMZW
      DXQGBWDXFBIZKWAXFWEJZWEDJZXQWAWCXSUFXSXTWDXFBVBVNWDYAXSWCYAWAWCYABDVOVPVN
      ZPAXFCWEDERSVDWAXJWCXPXRVIXKXOXQFGWBBCWMXGWFVKVLVQVMFWGWFQTWAXNWCAWECDEVF
      PAWHCWFDERSWDWAYAXLWFUQWLYBAWECDEVROVSVT $.
      $( [31-Jan-2015] $)

    $( Idempotence of closure under a pair union.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrcun $p |- ( ( C e. ( Moore ` X ) /\ U C_ X /\ V C_ X ) ->
        ( F ` ( U u. V ) ) = ( F ` ( ( F ` U ) u. ( F ` V ) ) ) ) $=
      ( cfv wcel wss cpr cuni cun wceq elpw2g syl biimpar syl2anc fveq2d fvex
      wb cmre w3a cima cpw simp1 mre1cl 3adant3 3adant2 prssi mrcuni uniprg wfn
      wf mrcf ffn 3ad2ant1 fnimapr syl3anc unieqd unipr syl6eq 3eqtr3d ) AEUAGH
      ZBEIZDEIZUBZBDJZKZCGZCVGUCZKZCGZBDLZCGBCGZDCGZLZCGVFVCVGEUDZIZVIVLMVCVDVE
      UEVFBVQHZDVQHZVRVCVDVSVEVCVSVDVCEAHZVSVDTAEUFZBEANOPUGZVCVEVTVDVCVTVEVCWA
      VTVETWBDEANOPUHZBDVQUIQAVGCEFUJQVFVHVMCVFVSVTVHVMMWCWDBDVQVQUKQRVFVKVPCVF
      VKVNVOJZKVPVFVJWEVFCVQULZVSVTVJWEMVCVDWFVEVCVQACUMWFACEFUNVQACUOOUPWCWDVQ
      BDCUQURUSVNVOBCSDCSUTVARVB $.
      $( [31-Jan-2015] $)
  $}

  ${
    $d W a b c $.  $d U a b c $.  $d K a b c $.  $d F a b c $.
    mrclsp.u $e |- U = ( LSubSp ` W ) $.
    mrclsp.k $e |- K = ( LSpan ` W ) $.
    mrclsp.f $e |- F = ( mrCls ` U ) $.
    $( Moore closure generalizes module span.  (Contributed by Stefan O'Rear,
       31-Jan-2015.) $)
    mrclsp $p |- ( W e. LMod -> K = F ) $=
      ( va vb clmod wcel cbs cfv cpw cv wss crab cint cmpt eqid lspfval mrcfval
      cmre wceq lssmre syl eqtr4d ) DJKZCHDLMZNHOIOPIAQRSZBIACUIDJHUITZEFUAUHAU
      IUCMKBUJUDUIADEUKUEHABUIIGUBUFUG $.
      $( [31-Jan-2015] $)
  $}

  ${
    $d J a b c $.  $d F a b c $.
    mrccls.f $e |- F = ( mrCls ` ( Clsd ` J ) ) $.
    $( Moore closure generalizes closure in a topology.  (Contributed by Stefan
       O'Rear, 31-Jan-2015.) $)
    mrccls $p |- ( J e. Top -> ( cls ` J ) = F ) $=
      ( va ctop wcel ccl cfv cuni cpw wss ccld crab cint cmpt eqid clsfval cmre
      vb cv wceq cldmre mrcfval syl eqtr4d ) BEFZBGHDBIZJDTSTKSBLHZMNOZADSBUGUG
      PZQUFUHUGRHFAUIUABUGUJUBDUHAUGSCUCUDUE $.
      $( [31-Jan-2015] $)
  $}

