$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Noetherian left modules I
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LNoeM $.

  $( Extend class notation with the class of Noetherian left modules. $)
  clnm $a class LNoeM $.

  ${
    $d i w $.

    $( A left-module is _Noetherian_ iff it is hereditarily finitely
       generated. $)
    df-lnm $a |- LNoeM = { w e. LMod | A. i e. ( LSubSp ` w )
        ( w |`s i ) e. LFinGen } $.
  $}

  ${
    $d w i M $.  $d w i S $.
    islnm.s $e |- S = ( LSubSp ` M ) $.
    $( Property of being a Noetherian left module.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    islnm $p |- ( M e. LNoeM <-> ( M e. LMod /\
        A. i e. S ( M |`s i ) e. LFinGen ) ) $=
      ( vw cv cress co clfig wcel clss wral clmod clnm wceq fveq2 syl6eqr oveq1
      cfv eleq1d raleqbidv df-lnm elrab2 ) EFZBFZGHZIJZBUDKSZLCUEGHZIJZBALECMNU
      DCOZUGUJBUHAUKUHCKSAUDCKPDQUKUFUIIUDCUEGRTUAEBUBUC $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d i g M $.  $d i g N $.  $d i g S $.  $d i g B $.
    islnm2.b $e |- B = ( Base ` M ) $.
    islnm2.s $e |- S = ( LSubSp ` M ) $.
    islnm2.n $e |- N = ( LSpan ` M ) $.
    $( Property of being a Noetherian left module with finite generation
       expanded in terms of spans.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    islnm2 $p |- ( M e. LNoeM <-> ( M e. LMod /\
        A. i e. S E. g e. ( ~P B i^i Fin ) i = ( N ` g ) ) ) $=
      ( clnm wcel clmod cv cress co clfig wral wa wceq wrex cfv cpw islnm eqid1
      cfn cin islssfg2 eqcom rexbii syl6bb ralbidva pm5.32i bitri ) EJKELKZEDMZ
      NOZPKZDBQZRUNUOCMFUAZSZCAUBUEUFZTZDBQZRBDEHUCUNURVCUNUQVBDBUNUOBKRUQUSUOS
      ZCVATVBABUOFEUPCUPUDHIGUGVDUTCVAUSUOUHUIUJUKULUM $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d M a $.  $d U a $.  $d S a $.  $d R a $.
    $( A Noetherian left module is a left module.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    lnmlmod $p |- ( M e. LNoeM -> M e. LMod ) $=
      ( va clnm wcel clmod cv cress co clfig clss cfv wral eqid islnm simplbi )
      ACDAEDABFGHIDBAJKZLPBAPMNO $.
      $( [12-Dec-2014] $)

    ${
      lnmlssfg.s $e |- S = ( LSubSp ` M ) $.
      lnmlssfg.r $e |- R = ( M |`s U ) $.
      $( A submodule of Noetherian module is finitely generated.  (Contributed
         by Stefan O'Rear, 1-Jan-2015.) $)
      lnmlssfg $p |- ( ( M e. LNoeM /\ U e. S ) -> R e. LFinGen ) $=
        ( va clnm wcel cv cress co clfig wral clmod islnm simprbi oveq2 syl6eqr
        wceq eleq1d rcla4v mpan9 ) DHIZDGJZKLZMIZGBNZCBIAMIZUDDOIUHBGDEPQUGUIGC
        BUECTZUFAMUJUFDCKLAUECDKRFSUAUBUC $.
        $( [1-Jan-2015] $)

      $( All submodules of a Noetherian module are Noetherian.  (Contributed by
         Stefan O'Rear, 1-Jan-2015.) $)
      lnmlsslnm $p |- ( ( M e. LNoeM /\ U e. S ) -> R e. LNoeM ) $=
        ( va clnm wcel wa clmod cress co clfig cfv sylan cin wceq wss eqid clss
        cv lnmlmod lsslmod cvv vex oveq1i ressress syl5eq mpan2 ad2antlr simpll
        wral incom cbs lssss adantl ressbas2 syl sseqtr4d df-ss sylib wb lsslss
        simprbda eqeltrd lnmlssfg syl2anc ralrimiva islnm sylanbrc ) DHIZCBIZJZ
        AKIZAGUBZLMZNIZGAUAOZUMAHIVLDKIZVMVODUCZBCDAFEUDPVNVRGVSVNVPVSIZJZVQDCV
        PQZLMZNVMVQWERZVLWBVMVPUEIZWFGUFVMWGJVQDCLMZVPLMWEAWHVPLFUGCVPDBUEUHUIU
        JUKWCVLWDBIWENIVLVMWBULWCWDVPBWCWDVPCQZVPCVPUNWCVPCSZWIVPRWCVPAUOOZCWBV
        PWKSVNVSVPWKAWKTVSTZUPUQVMCWKRZVLWBVMCDUOOZSWMBCWNDWNTZEUPCWNADFWOURUSU
        KUTVPCVAVBUIVNWBVPBIZWJVLVTVMWBWPWJJVCWABVSCVPDAFEWLVDPVEVFWEBWDDEWETVG
        VHVFVIVSGAWLVJVK $.
        $( [1-Jan-2015] $)
    $}

    $( A Noetherian left module is finitely generated.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    lnmfg $p |- ( M e. LNoeM -> M e. LFinGen ) $=
      ( clnm wcel cbs cfv cress co clfig eqid ressid clss lnmlmod lss1 lnmlssfg
      clmod syl mpdan eqeltrrd ) ABCZAADEZFGZAHTABTIZJSTAKEZCZUAHCSAOCUDALUCTAU
      BUCIZMPUAUCTAUEUAINQR $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d ph a b c $.  $d S a b c $.  $d U a b c $.  $d T a b c $.  $d F a b c $.
    $d D a b c $.  $d V a b c $.  $d P a b c $.  $d Z a b c $.  $d K a b c $.
    $d B a b c $.
    kercvrlsm.u $e |- U = ( LSubSp ` S ) $.
    kercvrlsm.p $e |- P = ( LSSum ` S ) $.
    kercvrlsm.z $e |- Z = ( 0g ` T ) $.
    kercvrlsm.k $e |- K = ( `' F " { Z } ) $.
    kercvrlsm.b $e |- B = ( Base ` S ) $.
    kercvrlsm.f $e |- ( ph -> F e. ( S LMHom T ) ) $.
    kercvrlsm.t $e |- ( ph -> T e. V ) $.
    kercvrlsm.d $e |- ( ph -> D e. U ) $.
    kercvrlsm.cv $e |- ( ph -> ( F " D ) = ran F ) $.
    $( The domain of a linear function is the subspace sum of the kernel and
       any subspace which covers the range.  (Contributed by Stefan O'Rear,
       24-Jan-2015.) $)
    kercvrlsm $p |- ( ph -> ( K P D ) = B ) $=
      ( va vb co wss clmod clmhm lmhmlmod1 syl lmhmkerlss syl2anc lsmcl syl3anc
      wcel lssss cv wa cfv wceq wrex cima crn wfn cbs eqid lmhmf fnfvelrn sylan
      wf ffn adantr eleqtrrd wb fvelimab mpbid wi csg cplusg cgrp simprl sselda
      lmodgrp adantrl grpnpcan ad2antrr eqcom cghm lmghm ghmeqker syl5bb biimpa
      syl22anc simplrr lsmelvalix syl32anc eqeltrrd anassrs rexlimdva mpd ssrdv
      ex eqssd ) AICDUCZBAXBGUMZXBBUDAEUEUMZIGUMZCGUMZXCAHEFUFUCUMZXDQEFHUGUHZA
      XGFJUMZXEQREFGHIJKONLUIUJZSDGICELMUKULGXBBEPLUNUHAUABXBAUAUOZBUMZXKXBUMZA
      XLUPZUBUOZHUQZXKHUQZURZUBCUSZXMXNXQHCUTZUMZXSXNXQHVAZXTAHBVBZXLXQYBUMABFV
      CUQZHVHZYCAXGXIYEQRBYDEFHJPYDVDVEUJBYDHVIUHZBXKHVFVGAXTYBURXLTVJVKXNYCCBU
      DZYAXSVLAYCXLYFVJAYGXLAXFYGSGCBEPLUNUHZVJUBBCXQHVMUJVNXNXRXMUBCAXLXOCUMZX
      RXMVOAXLYIUPZUPZXRXMYKXRUPZXKXOEVPUQZUCZXOEVQUQZUCZXKXBYKYPXKURZXRYKEVRUM
      ZXLXOBUMZYQAYRYJAXDYRXHEWAUHVJAXLYIVSZAYIYSXLACBXOYHVTWBZBYOEYMXKXOPYOVDZ
      YMVDZWCULVJYLXDIBUDZYGYNIUMZYIYPXBUMAXDYJXRXHWDAUUDYJXRAXEUUDXJGIBEPLUNUH
      WDAYGYJXRYHWDYKXRUUEXRXQXPURZYKUUEXPXQWEYKHEFWFUCUMZXIXLYSUUFUUEVLAUUGYJA
      XGUUGQEFHWGUHVJAXIYJRVJYTUUABEFXKHIYMXOJKPNOUUCWHWKWIWJAXLYIXRWLYODICBEYN
      XOPUUBMWMWNWOWTWPWQWRWTWSXA $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d ph x $.  $d X x $.  $d S x $.  $d A x $.  $d U x $.  $d Y x $.
    $d T x $.  $d F x $.
    lmhmfgima.y $e |- Y = ( T |`s ( F " A ) ) $.
    lmhmfgima.x $e |- X = ( S |`s A ) $.
    lmhmfgima.u $e |- U = ( LSubSp ` S ) $.
    lmhmfgima.xf $e |- ( ph -> X e. LFinGen ) $.
    lmhmfgima.a $e |- ( ph -> A e. U ) $.
    lmhmfgima.f $e |- ( ph -> F e. ( S LMHom T ) ) $.
    lmhmfgima.t $e |- ( ph -> T e. V ) $.
    $( A homomorphism maps finitely generated submodules to finitely generated
       submodules.  (Contributed by Stefan O'Rear, 24-Jan-2015.) $)
    lmhmfgima $p |- ( ph -> Y e. LFinGen ) $=
      ( clfig cfv cfn wcel vx cima cress co cv clspn wceq cbs cpw wrex clmod wb
      cin clmhm lmhmlmod1 syl eqid islssfg2 syl2anc mpbid wa adantr inss1 sseli
      wss elpwi adantl lmhmlsp syl3anc oveq2d lmhmlmod2 imassrn wf lmhmf syl5ss
      crn frn cres wfo inss2 wfun cdm ffun sseqtr4d fores fofi islssfgi eqeltrd
      fdm imaeq2 eleq1d syl5ibcom rexlimdva mpd syl5eqel ) AIDFBUBZUCUDZQJAUAUE
      ZCUFRZRZBUGZUACUHRZUIZSUMZUJZWQQTZAHQTZXEMACUKTZBETXGXEULAFCDUNUDTZXHOCDF
      UOUPNXBEBWSCHUAKLWSUQZXBUQZURUSUTAXAXFUAXDAWRXDTZVAZDFWTUBZUCUDZQTXAXFXMX
      ODFWRUBZDUFRZRZUCUDZQXMXNXRDUCXMXIDGTZWRXBVEZXNXRUGAXIXLOVBAXTXLPVBXLYAAX
      LWRXCTYAXDXCWRXCSVCVDWRXBVFUPVGZCDWRFWSXQXBGXKXJXQUQZVHVIVJXMDUKTZXPDUHRZ
      VEZXPSTZXSQTAYDXLAXIXTYDOPCDFGVKUSVBAYFXLAXPFVPZYEFWRVLAXBYEFVMZYHYEVEAXI
      XTYIOPXBYECDFGXKYEUQZVNUSZXBYEFVQUPVOVBXMWRSTZWRXPFWRVRZVSZYGXLYLAXDSWRXC
      SVTVDVGXMFWAZWRFWBZVEYNAYOXLAYIYOYKXBYEFWCUPVBXMWRXBYPYBAYPXBUGZXLAYIYQYK
      XBYEFWIUPVBWDWRFWEUSWRXPYMWFUSXPXQYEDXSYCYJXSUQWGVIWHXAXOWQQXAXNWPDUCWTBF
      WJVJWKWLWMWNWO $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d V a $.  $d T a $.  $d S a $.  $d F a $.  $d B a $.
    lnmepi.b $e |- B = ( Base ` T ) $.
    $( Epimorphic images of Noetherian modules are Noetherian.  (Contributed by
       Stefan O'Rear, 24-Jan-2015.) $)
    lnmepi $p |- ( ( ( F e. ( S LMHom T ) /\ T e. V ) /\
        ( S e. LNoeM /\ ran F = B ) ) -> T e. LNoeM ) $=
      ( va co wcel wa clnm wceq cress clfig clss cfv adantr cima eqid sylanbrc
      clmhm crn clmod cv wral lmhmlmod2 cbs wfo wss wf lmhmf simprr dffo2 lssss
      foimacnv syl2an oveq2d simplrl simpll lmhmpreima lnmlssfg syl2anc simplll
      ccnv sylan simpllr lmhmfgima eqeltrrd ralrimiva islnm ) DBCUAHIZCEIZJZBKI
      ZDUBALZJZJZCUCIZCGUDZMHZNIZGCOPZUECKIVMVRVPBCDEUFQVQWAGWBVQVSWBIZJZCDDVDV
      SRZRZMHZVTNWDWFVSCMVQBUGPZADUHZVSAUIWFVSLWCVQWHADUJZVOWIVMWJVPWHABCDEWHSF
      UKQVMVNVOULWHADUMTWBVSACFWBSZUNWHAVSDUOUPUQWDWEBCBOPZDEBWEMHZWGWGSWMSZWLS
      ZWDVNWEWLIZWMNIVMVNVOWCURVQVKWCWPVKVLVPUSBCVSDWLWBWOWKUTVEZWMWLWEBWOWNVAV
      BWQVKVLVPWCVCVKVLVPWCVFVGVHVIWBGCWKVJT $.
      $( [24-Jan-2015] $)
  $}

  ${
    $d F a b $.  $d S a b $.  $d T a b $.  $d K a b $.  $d U a b $.
    $d V a b $.
    lmhmfgsplit.z $e |- Z = ( 0g ` T ) $.
    lmhmfgsplit.k $e |- K = ( `' F " { Z } ) $.
    lmhmfgsplit.u $e |- U = ( S |`s K ) $.
    lmhmfgsplit.v $e |- V = ( T |`s ran F ) $.
    $( If the kernel and range of a homomorphism of left modules are finitely
       generated, then so is the domain.  (Contributed by Stefan O'Rear,
       1-Jan-2015.) $)
    lmhmfgsplit $p |- ( ( F e. ( S LMHom T ) /\ U e. LFinGen /\
          V e. LFinGen ) -> S e. LFinGen ) $=
      ( cvv wcel co clfig c0 cress wa cfv eqid va vb clmhm w3a neirr clmod cgrp
      wn wne fglmod lmodgrp grpn0 mto crn reldmress ovprc1 syl5eq eleq1d mtbiri
      3syl con4i 3ad2ant3 cv cfn clspn wceq cpw wrex simprr wb lmhmlmod2 ancoms
      clss adantr lmhmrnlss islssfg syl2anc mpbid cima cbs cin wfn wss wf lmhmf
      ffn syl ad2antrr fvex a1i ad2antrl simprrl fipreima syl22anc clsm simp1lr
      elpwi 3expa simp1ll lmhmlmod1 ad2antlr inss1 sseli lspcl lmhmlsp ad2antll
      syl3anc fveq2 simp2rr 3eqtrd kercvrlsm oveq2d ressid eqtr2d simp1rl inss2
      lmhmkerlss islssfgi lsmfgcl eqeltrd expr rexlimdva mpd exp43 3impd mpcom
      ) BLMZDABUCNMZCOMZFOMZUDAOMZYJYHYGYIYGYJYGUHZYJPOMZYMPPUIZPUEYMPUFMPUGMYN
      PUJPUKPULUTUMYLFPOYLFBDUNZQNPKBYOQUOUPUQURUSVAVBYGYHYIYJYKYGYHYIYJYKYGYHR
      ZYIYJRZRZUAVCZVDMZYSBVESZSZYOVFZRZUAYOVGZVHZYKYRYJUUFYPYIYJVIYRBUFMZYOBVM
      SZMZYJUUFVJYPUUGYQYHYGUUGABDLVKVLVNYPUUIYQYHYGUUIABDLVOVLVNUUHYOUUABFUAKU
      UHTUUATZVPVQVRYRUUDYKUAUUEYRYSUUEMZUUDYKYRUUKUUDRZRZDUBVCZVSZYSVFZUBAVTSZ
      VGZVDWAZVHZYKUUMDUUQWBZUUQLMZYSYOWCZYTUUTYPUVAYQUULYHYGUVAYHYGRUUQBVTSZDW
      DUVAUUQUVDABDLUUQTZUVDTWEUUQUVDDWFWGVLWHUVBUUMAVTWIWJUUKUVCYRUUDYSYOWQWKY
      RUUKYTUUCWLYSUUQDLUBWMWNUUMUUPYKUBUUSUUMUUNUUSMZUUPYKUUMUVFUUPRZRZAAEUUNA
      VESZSZAWOSZNZQNZOUVHUVMAUUQQNZAUVHUVLUUQAQUVHUUQUVJUVKABAVMSZDELGUVOTZUVK
      TZHIUVEYRUULUVGYHYGYHYQUULUVGWPWRZYRUULUVGYGYGYHYQUULUVGWSWRZUVHAUFMZUUNU
      UQWCZUVJUVOMYRUVTUULUVGYHUVTYGYQABDWTXAZWHZUVFUWAUUMUUPUVFUUNUURMUWAUUSUU
      RUUNUURVDXBXCUUNUUQWQWGWKZUVOUUNUVIUUQAUVEUVPUVITZXDVQZUVHDUVJVSZUUOUUASZ
      UUBYOUVHYHYGUWAUWGUWHVFUVRUVSUWDABUUNDUVIUUAUUQLUVEUWEUUJXEXGUUPUWHUUBVFU
      UMUVFUUOYSUUAXHXFYRUULUVGUUCYTUUCUUKYRUVGXIWRXJXKXLYRUVNAVFZUULUVGYRUVTUW
      IUWBUUQAUFUVEXMWGWHXNUVHEUVJCUVKUVOAUVJQNZUVMAUVPUVQJUWJTZUVMTUWCYREUVOMZ
      UULUVGYPUWLYQYHYGUWLABUVODELGIHUVPXQVLVNWHUWFYRUULUVGYIYIYJYPUULUVGXOWRUV
      HUVTUWAUUNVDMZUWJOMUWCUWDUVFUWMUUMUUPUUSVDUUNUURVDXPXCWKUUNUVIUUQAUWJUWEU
      VEUWKXRXGXSXTYAYBYCYAYBYCYDYEYF $.
      $( [1-Jan-2015] $)

    $( If the kernel and range of a homomorphism of left modules are
       Noetherian, then so is the domain.  (Contributed by Stefan O'Rear,
       1-Jan-2015.) $)
    lmhmlnmsplit $p |- ( ( F e. ( S LMHom T ) /\ U e. LNoeM /\ V e. LNoeM ) ->
        S e. LNoeM ) $=
      ( co wcel cress clfig cvv c0 eqid cin syl2anc va clmhm clnm clmod cv clss
      w3a cfv wral lmhmlmod1 3ad2ant1 wa cres ccnv csn cima crn simpl1 wn neirr
      wne cgrp lnmlmod lmodgrp grpn0 3syl reldmress ovprc1 syl5eq eleq1d mtbiri
      con4i 3ad2ant3 adantr simpr reslmhm syl3anc cnvresima eqcomi ineq1i eqtri
      mto oveq2i incom inidm ineq2i 3eqtr4i in12 wceq vex inex2 ressress oveq1i
      mp2an a1i cnvexg imaexg syl syl5eqel inex1 sylancl 3eqtr4a wss lmhmkerlss
      simpl2 lssincl inss2 lsslss mpbir2and lnmlssfg eqeltrd rnexg resexg resss
      wb rnss ax-mp df-ss mpbi simpl3 lmhmlmod2 lmhmrnlss lmhmfgsplit ralrimiva
      syl6reqr islnm sylanbrc ) DABUBLZMZCUCMZFUCMZUGZAUDMZAUAUEZNLZOMZUAAUFUHZ
      UIAUCMYIYJYMYKABDUJUKZYLYPUAYQYLYNYQMZULZDYNUMZYOBUBLMZYOUUAUNGUOZUPZNLZO
      MBUUAUQZNLZOMYPYTYIBPMZYSUUBYIYJYKYSURZYLUUHYSYKYIUUHYJUUHYKUUHUSZYKQUCMZ
      UUKQQVAZQUTUUKQUDMQVBMUULQVCQVDQVEVFWBUUJFQUCUUJFBDUQZNLZQKBUUMNVGVHVIVJV
      KVLVMVNZYLYSVOZYOABYQDPYNYQRZYORVPVQZYTUUECYNESZNLZOYTUUEYOEYNSZNLZUUTUUD
      UVAYONUUDDUNZUUCUPZYNSUVAYNUUCDVRUVDEYNEUVDIVSVTWAWCYTAYNUVASZNLZAEUUSSZN
      LZUVBUUTUVEUVGANEYNYNSZSZYNEESZSZUVEUVGUVAUUSUVJUVLEYNWDUVIYNEYNWEWFUVKEY
      NEWEWFWGYNEYNWHEYNEWHWGWCUVBUVFWIZYTYNPMUVAPMUVMUAWJZYNEUVNWKYNUVAAPPWLWN
      WOYTUUTAENLZUUSNLZUVHCUVOUUSNJWMYTEPMZUUSPMUVPUVHWIYTYIUVQUUIYIEUVDPIYIUV
      CPMUVDPMDYHWPUVCUUCPWQWRWSWRYNEUVNWTEUUSAPPWLXAVIXBVIYTYJUUSCUFUHZMZUUTOM
      YIYJYKYSXEYTUVSUUSYQMZUUSEXCZYTYMYSEYQMZUVTYLYMYSYRVNZUUPYTYIUUHUWBUUIUUO
      ABYQDEPGIHUUQXDTZYQYNEAUUQXFVQUWAYTYNEXGWOYTYMUWBUVSUVTUWAULXOUWCUWDYQUVR
      EUUSACJUUQUVRRZXHTXIUUTUVRUUSCUWEUUTRXJTXKYTUUGFUUFNLZOYTYIUUGUWFWIUUIYIU
      WFBUUMUUFSZNLZUUGYIUWFUUNUUFNLZUWHFUUNUUFNKWMYIUUMPMUUFPMZUWIUWHWIDYHXLYI
      UUAPMUWJDYNYHXMUUAPXLWRUUMUUFBPPWLTVIUUFUWGBNUWGUUFUWGUUFUUMSZUUFUUMUUFWD
      UUFUUMXCZUWKUUFWIUUADXCUWLDYNXNUUADXPXQZUUFUUMXRXSWAVSWCYEWRYTYKUUFFUFUHZ
      MZUWFOMYIYJYKYSXTYTUWOUUFBUFUHZMZUWLYTUUBBUDMZUWQUURYTYIUUHUWRUUIUUOABDPY
      ATZYOBUUAUDYBTUWLYTUWMWOYTUWRUUMUWPMZUWOUWQUWLULXOUWSYTYIUUHUWTUUIUUOABDP
      YBTUWPUWNUUMUUFBFKUWPRUWNRZXHTXIUWFUWNUUFFUXAUWFRXJTXKYOBUUEUUAUUDUUGGHUU
      DRUUERUUGRYCVQYDYQUAAUUQYFYG $.
      $( [1-Jan-2015] $)
  $}

  ${
    $d R a $.  $d S a $.  $d V a $.
    $( Noetherian is an invariant property of modules.  (Contributed by Stefan
       O'Rear, 25-Jan-2015.) $)
    lnmlmic $p |- ( ( R ~=m S /\ S e. V ) ->
        ( R e. LNoeM <-> S e. LNoeM ) ) $=
      ( va wcel clnm clmim co wa clmhm crn cbs cfv wceq lmimlmhm ad2antrr simpr
      simplr adantr clmod clmic wbr wb cv wex wi c0 wne brlmic n0 wf1o wfo eqid
      bitri lmimf1o f1ofo forn 3syl lnmepi syl22anc lmimcnv lmhmlmod1 syl sylan
      ccnv jca impbida ex exlimiv sylbi imp ) ABUAUBZBCEZAFEZBFEZUCZVLDUDZABGHZ
      EZDUEZVMVPUFZVLVRUGUHVTABUIDVRUJUNVSWADVSVMVPVSVMIZVNVOWBVNIVQABJHEZVMVNV
      QKBLMZNZVOVSWCVMVNABVQOZPVSVMVNRWBVNQWBWEVNWBALMZWDVQUKWGWDVQULWEWGWDABVQ
      CWGUMZWDUMZUOWGWDVQUPWGWDVQUQURSWDABVQCWIUSUTWBVQVEZBAGHEZATEZIZVOVNWBWKW
      LABVQCVAVSWLVMVSWCWLWFABVQVBVCSVFWMVOIWJBAJHEZWLVOWJKWGNZVNWKWNWLVOBAWJOP
      WKWLVORWMVOQWMWOVOWMWDWGWJUKWDWGWJULWOWDWGBAWJTWIWHUOWDWGWJUPWDWGWJUQURSW
      GBAWJTWHUSUTVDVGVHVIVJVK $.
      $( [25-Jan-2015] $)
  $}

