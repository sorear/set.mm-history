$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Noetherian left modules I
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LNoeM $.

  $( Extend class notation with the class of Noetherian left modules. $)
  clnm $a class LNoeM $.

  ${
    $d i w $.

    $( A left-module is _Noetherian_ iff it is hereditarily finitely
       generated. $)
    df-lnm $a |- LNoeM = { w e. LMod | A. i e. ( LSubSp ` w )
        ( w |`s i ) e. LFinGen } $.
  $}

  ${
    $d w i M $.  $d w i S $.
    islnm.s $e |- S = ( LSubSp ` M ) $.
    $( Property of being a Noetherian left module.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    islnm $p |- ( M e. LNoeM <-> ( M e. LMod /\
        A. i e. S ( M |`s i ) e. LFinGen ) ) $=
      ( vw cv cress co clfig wcel clss wral clmod clnm wceq fveq2 syl6eqr oveq1
      cfv eleq1d raleqbidv df-lnm elrab2 ) EFZBFZGHZIJZBUDKSZLCUEGHZIJZBALECMNU
      DCOZUGUJBUHAUKUHCKSAUDCKPDQUKUFUIIUDCUEGRTUAEBUBUC $.
      $( [12-Dec-2014] $)
  $}

  ${
    $d M a $.  $d U a $.  $d S a $.  $d R a $.
    $( A Noetherian left module is a left module.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    lnmlmod $p |- ( M e. LNoeM -> M e. LMod ) $=
      ( va clnm wcel clmod cv cress co clfig clss cfv wral eqid islnm simplbi )
      ACDAEDABFGHIDBAJKZLPBAPMNO $.
      $( [12-Dec-2014] $)

    ${
      lnmlssfg.s $e |- S = ( LSubSp ` M ) $.
      lnmlssfg.r $e |- R = ( M |`s U ) $.
      $( A submodule of Noetherian module is finitely generated.  (Contributed
         by Stefan O'Rear, 1-Jan-2015.) $)
      lnmlssfg $p |- ( ( M e. LNoeM /\ U e. S ) -> R e. LFinGen ) $=
        ( va clnm wcel cv cress co clfig wral clmod islnm simprbi oveq2 syl6eqr
        wceq eleq1d rcla4v mpan9 ) DHIZDGJZKLZMIZGBNZCBIAMIZUDDOIUHBGDEPQUGUIGC
        BUECTZUFAMUJUFDCKLAUECDKRFSUAUBUC $.
        $( [1-Jan-2015] $)

      $( All submodules of a Noetherian module are Noetherian.  (Contributed by
         Stefan O'Rear, 1-Jan-2015.) $)
      lnmlsslnm $p |- ( ( M e. LNoeM /\ U e. S ) -> R e. LNoeM ) $=
        ( va clnm wcel wa clmod cress co clfig cfv sylan cin wceq wss eqid clss
        cv lnmlmod lsslmod cvv vex oveq1i ressress syl5eq mpan2 ad2antlr simpll
        wral incom cbs lssss adantl ressbas2 syl sseqtr4d df-ss sylib wb lsslss
        simprbda eqeltrd lnmlssfg syl2anc ralrimiva islnm sylanbrc ) DHIZCBIZJZ
        AKIZAGUBZLMZNIZGAUAOZUMAHIVLDKIZVMVODUCZBCDAFEUDPVNVRGVSVNVPVSIZJZVQDCV
        PQZLMZNVMVQWERZVLWBVMVPUEIZWFGUFVMWGJVQDCLMZVPLMWEAWHVPLFUGCVPDBUEUHUIU
        JUKWCVLWDBIWENIVLVMWBULWCWDVPBWCWDVPCQZVPCVPUNWCVPCSZWIVPRWCVPAUOOZCWBV
        PWKSVNVSVPWKAWKTVSTZUPUQVMCWKRZVLWBVMCDUOOZSWMBCWNDWNTZEUPCWNADFWOURUSU
        KUTVPCVAVBUIVNWBVPBIZWJVLVTVMWBWPWJJVCWABVSCVPDAFEWLVDPVEVFWEBWDDEWETVG
        VHVFVIVSGAWLVJVK $.
        $( [1-Jan-2015] $)
    $}

    $( A Noetherian left module is finitely generated.  (Contributed by Stefan
       O'Rear, 12-Dec-2014.) $)
    lnmfg $p |- ( M e. LNoeM -> M e. LFinGen ) $=
      ( clnm wcel cbs cfv cress co clfig eqid ressid clss lnmlmod lss1 lnmlssfg
      clmod syl mpdan eqeltrrd ) ABCZAADEZFGZAHTABTIZJSTAKEZCZUAHCSAOCUDALUCTAU
      BUCIZMPUAUCTAUEUAINQR $.
      $( [12-Dec-2014] $)
  $}

  $( No class is unequal to itself.  (Contributed by Stefan O'Rear,
     1-Jan-2015.) $)
  neirr $p |- -. A =/= A $=
    ( wne wn wceq eqid nne mpbir ) AABCAADAEAAFG $.
    $( [1-Jan-2015] $)

  ${
    $d ph a b c $.  $d F a b c $.  $d S a b c $.  $d T a b c $.  $d A a b c $.
    $d B a b c $.  $d K a b c $.
    lmhmfgsplitlem.f $e |- ( ph -> F e. ( S LMHom T ) ) $.
    lmhmfgsplitlem.s $e |- ( ph -> S e. LMod ) $.
    lmhmfgsplitlem.t $e |- ( ph -> T e. LMod ) $.
    lmhmfgsplitlem.k $e |- K = ( `' F " { ( 0g ` T ) } ) $.
    lmhmfgsplitlem.af $e |- ( ph -> A e. Fin ) $.
    lmhmfgsplitlem.as $e |- ( ph -> A C_ K ) $.
    lmhmfgsplitlem.ak $e |- ( ph -> ( ( LSpan ` S ) ` A ) = K ) $.
    lmhmfgsplitlem.bf $e |- ( ph -> B e. Fin ) $.
    lmhmfgsplitlem.bs $e |- ( ph -> B C_ ran F ) $.
    lmhmfgsplitlem.bk $e |- ( ph -> ( ( LSpan ` T ) ` B ) = ran F ) $.
    $( Lemma for ~ lmhmfgsplit . $)
    lmhmfgsplitlem $p |- ( ph -> S e. LFinGen ) $=
      ( wceq cfv wcel va vb vc cv cima cbs cpw cfn cin clfig wfn cvv crn wss wf
      wrex clmhm co clmod eqid lmhmf syl2anc ffn syl fvex a1i fipreima syl22anc
      wa wi elin vex elpw anbi1i bitri w3a cun simp2l ccnv c0g csn cdm cnvimass
      clspn fdm syl5sseq syl5eqss sstrd 3ad2ant1 unssd elpw2 sylibr simp2r unfi
      lspssv fnfvelrn sylan lmhmlsp syl3anc simp3 fveq2d 3eqtrd adantr eleqtrrd
      wb fvelimab mpbid csg cplusg cgrp lmodgrp ad2antrr simplr simprl grpnpcan
      sseldd clss lspcl eqcomd ssun2 lspss eqsstrd eqcom sselda ghmeqker syl5bb
      cghm lmghm biimpd impr ssun1 lssvacl eqeltrrd exp32 rexlimdv mpd ex ssrdv
      eqssd eleq1 fveq2 eqeq1d anbi12d rcla4ev syl12anc islmodfgOLD mpbird 3exp
      syl5bi ) AFUAUDZUEZCRZUADUFSZUGZUHUIZUPZDUJTZAFUUMUKZUUMULTZCFUMZUNCUHTUU
      PAUUMEUFSZFUOZUURAFDEUQURTZEUSTZUVBHJUUMUVADEFUSUUMUTZUVAUTVAVBZUUMUVAFVC
      VDZUUSADUFVEZVFPOCUUMFULUAVGVHAUULUUQUAUUOUUJUUOTZUUJUUMUNZUUJUHTZVIZAUUL
      UUQVJUVIUUJUUNTZUVKVIUVLUUJUUNUHVKUVMUVJUVKUUJUUMUAVLVMVNVOAUVLUULUUQAUVL
      UULVPZUUQUBUDZUHTZUVODWDSZSZUUMRZVIZUBUUNUPZUVNUUJBVQZUUNTZUWBUHTZUWBUVQS
      ZUUMRZUWAUVNUWBUUMUNZUWCUVNUUJBUUMAUVJUVKUULVRZAUVLBUUMUNUULABGUUMMAGFVSE
      VTSZWAZUEZUUMKAFWBZUWKUUMFUWJWCAUVBUWLUUMRUVFUUMUVAFWEVDWFWGWHWIWJZUWBUUM
      UVHWKWLUVNUVKBUHTZUWDAUVJUVKUULWMAUVLUWNUULLWIUUJBWNVBUVNUWEUUMUVNDUSTZUW
      GUWEUUMUNAUVLUWOUULIWIZUWMUWBUVQUUMDUVEUVQUTZWOVBUVNUBUUMUWEUVNUVOUUMTZUV
      OUWETZUVNUWRVIZUCUDZFSZUVOFSZRZUCUUJUVQSZUPZUWSUWTUXCFUXEUEZTZUXFUWTUXCUU
      TUXGUVNUURUWRUXCUUTTAUVLUURUULUVGWIZUUMUVOFWPWQUVNUXGUUTRUWRUVNUXGUUKEWDS
      ZSZCUXJSZUUTUVNUVCUVDUVJUXGUXKRAUVLUVCUULHWIAUVLUVDUULJWIZUWHDEUUJFUVQUXJ
      UUMUSUVEUWQUXJUTWRWSUVNUUKCUXJAUVLUULWTXAAUVLUXLUUTRUULQWIXBXCXDUWTUURUXE
      UUMUNZUXHUXFXEUVNUURUWRUXIXCUVNUXNUWRUVNUWOUVJUXNUWPUWHUUJUVQUUMDUVEUWQWO
      VBZXCZUCUUMUXEUXCFXFVBXGUWTUXDUWSUCUXEUWTUXAUXETZUXDUWSUWTUXQUXDVIZVIZUVO
      UXADXHSZURZUXADXISZURZUVOUWEUXSDXJTZUWRUXAUUMTZUYCUVORUVNUYDUWRUXRUVNUWOU
      YDUWPDXKVDXLUVNUWRUXRXMUXSUXEUUMUXAUVNUXNUWRUXRUXOXLUWTUXQUXDXNZXPUUMUYBD
      UXTUVOUXAUVEUYBUTZUXTUTZXOWSUXSUWOUWEDXQSZTZUYAUWETUXAUWETUYCUWETUVNUWOUW
      RUXRUWPXLUVNUYJUWRUXRUVNUWOUWGUYJUWPUWMUYIUWBUVQUUMDUVEUYIUTZUWQXRVBXLUXS
      GUWEUYAUVNGUWEUNUWRUXRUVNGBUVQSZUWEAUVLGUYLRUULAUYLGNXSWIUVNUWOUWGBUWBUNZ
      UYLUWEUNUWPUWMUYMUVNBUUJXTVFBUWBUVQUUMDUVEUWQYAWSYBXLUWTUXQUXDUYAGTZUWTUX
      QVIZUXDUYNUXDUXCUXBRZUYOUYNUXBUXCYCUYOFDEYGURTZUVDUWRUYEUYPUYNXEUVNUYQUWR
      UXQAUVLUYQUULAUVCUYQHDEFYHVDWIXLUVNUVDUWRUXQUXMXLUVNUWRUXQXMUWTUXEUUMUXAU
      XPYDUUMDEUVOFGUXTUXAUSUWIUVEUWIUTKUYHYEVHYFYIYJXPUXSUXEUWEUXAUVNUXEUWEUNZ
      UWRUXRUVNUWOUWGUUJUWBUNZUYRUWPUWMUYSUVNUUJBYKVFUUJUWBUVQUUMDUVEUWQYAWSXLU
      YFXPUYBUYIUWEDUYAUXAUYGUYKYLVHYMYNYOYPYQYRYSUVTUWDUWFVIUBUWBUUNUVOUWBRZUV
      PUWDUVSUWFUVOUWBUHYTUYTUVRUWEUUMUVOUWBUVQUUAUUBUUCUUDUUEUVNUWOUUQUWAXEUWP
      UUMDUBUVEUUFVDUUGUUHUUIYOYP $.
      $( [11-Dec-2014] $)
  $}

  ${
    $d F a b $.  $d S a b $.  $d T a b $.  $d K a b $.  $d U a b $.
    $d V a b $.
    lmhmfgsplit.z $e |- Z = ( 0g ` T ) $.
    lmhmfgsplit.k $e |- K = ( `' F " { Z } ) $.
    lmhmfgsplit.u $e |- U = ( S |`s K ) $.
    lmhmfgsplit.v $e |- V = ( T |`s ran F ) $.
    $( If the kernel and range of a homomorphism of left modules are finitely
       generated, then so is the domain.  (Contributed by Stefan O'Rear,
       1-Jan-2015.) $)
    lmhmfgsplit $p |- ( ( F e. ( S LMHom T ) /\ U e. LFinGen /\
          V e. LFinGen ) -> S e. LFinGen ) $=
      ( va vb cvv wcel clfig c0 wa cfv eqid clmhm co w3a wne neirr clmod fglmod
      wn cgrp lmodgrp grpn0 mto crn cress reldmress ovprc1 syl5eq eleq1d mtbiri
      3syl con4i 3ad2ant3 wi cv cfn clspn wceq cpw wrex wb lmhmlmod1 lmhmkerlss
      clss adantr islssfg syl2anc lmhmlmod2 lmhmrnlss anbi12d simplll ad3antrrr
      ad2antrr ccnv csn cima c0g sneqi imaeq2i eqtri ad2antlr wss simplrl elpwi
      simprl syl simprr simprrl ad2antrl simprrr lmhmfgsplitlem rexlimdv sylbid
      exp32 imp3a ancoms exp4b 3impd mpcom ) BNOZDABUAUBOZCPOZFPOZUCAPOZXLXJXIX
      KXIXLXIUHZXLQPOZXOQQUDZQUEXOQUFOQUIOXPQUGQUJQUKUTULXNFQPXNFBDUMZUNUBQKBXQ
      UNUOUPUQURUSVAVBXIXJXKXLXMXIXJXKXLXMXJXIXKXLRZXMVCXJXIRZXRLVDZVEOZXTAVFSZ
      SEVGZRZLEVHZVIZMVDZVEOZYGBVFSZSXQVGZRZMXQVHZVIZRXMXSXKYFXLYMXSAUFOZEAVMSZ
      OXKYFVJXJYNXIABDVKZVNABYODENGIHYOTZVLYOEYBACLJYQYBTVOVPXSBUFOZXQBVMSZOXLY
      MVJABDNVQZABDNVRYSXQYIBFMKYSTYITVOVPVSXSYFYMXMXSYDYMXMVCZLYEXSXTYEOZYDUUA
      XSUUBYDRZRZYKXMMYLUUDYGYLOZYKXMUUDUUEYKRZRZXTYGABDEXJXIUUCUUFVTXJYNXIUUCU
      UFYPWAXSYRUUCUUFYTWBEDWCZGWDZWEUUHBWFSZWDZWEIUUIUUKUUHGUUJHWGWHWIUUCYAXSU
      UFUUBYAYCWNWJUUGUUBXTEWKXSUUBYDUUFWLXTEWMWOUUCYCXSUUFUUBYAYCWPWJUUDUUEYHY
      JWQUUEYGXQWKUUDYKYGXQWMWRUUDUUEYHYJWSWTXCXAXCXAXDXBXEXFXGXH $.
      $( [1-Jan-2015] $)

    $( If the kernel and range of a homomorphism of left modules are
       Noetherian, then so is the domain.  (Contributed by Stefan O'Rear,
       1-Jan-2015.) $)
    lmhmlnmsplit $p |- ( ( F e. ( S LMHom T ) /\ U e. LNoeM /\ V e. LNoeM ) ->
        S e. LNoeM ) $=
      ( co wcel cress clfig cvv c0 eqid cin syl2anc va clmhm clnm clmod cv clss
      w3a cfv wral lmhmlmod1 3ad2ant1 wa cres ccnv csn cima crn simpl1 wn neirr
      wne cgrp lnmlmod lmodgrp grpn0 3syl reldmress ovprc1 syl5eq eleq1d mtbiri
      con4i 3ad2ant3 adantr simpr reslmhm syl3anc cnvresima eqcomi ineq1i eqtri
      mto oveq2i incom inidm ineq2i 3eqtr4i in12 wceq vex inex2 ressress oveq1i
      mp2an a1i cnvexg imaexg syl syl5eqel inex1 sylancl 3eqtr4a wss lmhmkerlss
      simpl2 lssincl inss2 lsslss mpbir2and lnmlssfg eqeltrd rnexg resexg resss
      wb rnss ax-mp df-ss mpbi simpl3 lmhmlmod2 lmhmrnlss lmhmfgsplit ralrimiva
      syl6reqr islnm sylanbrc ) DABUBLZMZCUCMZFUCMZUGZAUDMZAUAUEZNLZOMZUAAUFUHZ
      UIAUCMYIYJYMYKABDUJUKZYLYPUAYQYLYNYQMZULZDYNUMZYOBUBLMZYOUUAUNGUOZUPZNLZO
      MBUUAUQZNLZOMYPYTYIBPMZYSUUBYIYJYKYSURZYLUUHYSYKYIUUHYJUUHYKUUHUSZYKQUCMZ
      UUKQQVAZQUTUUKQUDMQVBMUULQVCQVDQVEVFWBUUJFQUCUUJFBDUQZNLZQKBUUMNVGVHVIVJV
      KVLVMVNZYLYSVOZYOABYQDPYNYQRZYORVPVQZYTUUECYNESZNLZOYTUUEYOEYNSZNLZUUTUUD
      UVAYONUUDDUNZUUCUPZYNSUVAYNUUCDVRUVDEYNEUVDIVSVTWAWCYTAYNUVASZNLZAEUUSSZN
      LZUVBUUTUVEUVGANEYNYNSZSZYNEESZSZUVEUVGUVAUUSUVJUVLEYNWDUVIYNEYNWEWFUVKEY
      NEWEWFWGYNEYNWHEYNEWHWGWCUVBUVFWIZYTYNPMUVAPMUVMUAWJZYNEUVNWKYNUVAAPPWLWN
      WOYTUUTAENLZUUSNLZUVHCUVOUUSNJWMYTEPMZUUSPMUVPUVHWIYTYIUVQUUIYIEUVDPIYIUV
      CPMUVDPMDYHWPUVCUUCPWQWRWSWRYNEUVNWTEUUSAPPWLXAVIXBVIYTYJUUSCUFUHZMZUUTOM
      YIYJYKYSXEYTUVSUUSYQMZUUSEXCZYTYMYSEYQMZUVTYLYMYSYRVNZUUPYTYIUUHUWBUUIUUO
      ABYQDEPGIHUUQXDTZYQYNEAUUQXFVQUWAYTYNEXGWOYTYMUWBUVSUVTUWAULXOUWCUWDYQUVR
      EUUSACJUUQUVRRZXHTXIUUTUVRUUSCUWEUUTRXJTXKYTUUGFUUFNLZOYTYIUUGUWFWIUUIYIU
      WFBUUMUUFSZNLZUUGYIUWFUUNUUFNLZUWHFUUNUUFNKWMYIUUMPMUUFPMZUWIUWHWIDYHXLYI
      UUAPMUWJDYNYHXMUUAPXLWRUUMUUFBPPWLTVIUUFUWGBNUWGUUFUWGUUFUUMSZUUFUUMUUFWD
      UUFUUMXCZUWKUUFWIUUADXCUWLDYNXNUUADXPXQZUUFUUMXRXSWAVSWCYEWRYTYKUUFFUFUHZ
      MZUWFOMYIYJYKYSXTYTUWOUUFBUFUHZMZUWLYTUUBBUDMZUWQUURYTYIUUHUWRUUIUUOABDPY
      ATZYOBUUAUDYBTUWLYTUWMWOYTUWRUUMUWPMZUWOUWQUWLULXOUWSYTYIUUHUWTUUIUUOABDP
      YBTUWPUWNUUMUUFBFKUWPRUWNRZXHTXIUWFUWNUUFFUXAUWFRXJTXKYOBUUEUUAUUDUUGGHUU
      DRUUERUUGRYCVQYDYQUAAUUQYFYG $.
      $( [1-Jan-2015] $)
  $}

