$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Miscellanea for Lagrange's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Two elements in a half-open interval have separation _strictly_ less
       than the difference between the endpoints.  (Contributed by Stefan
       O'Rear, 12-Sep-2014.) $)
    icodiamlt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. ( A [,) B ) /\ D e. (
        A [,) B ) ) ) -> ( abs ` ( C - D ) ) < ( B - A ) ) $=
      ( cr wcel wa co cmin clt wbr cle w3a elico2 wb resubcl syl2anc cc syl3anc
      mpbid cico cabs cfv cxr wi rexr anbi12d biimpd sylan2 cneg simprl1 simplr
      simprr1 simpll abslt wceq negsubdi2 simprl2 lesub1 simprr3 ltsub2 lelttrd
      recnd eqbrtrd simprl3 ltsub1 simprr2 lesub2 ltletrd mpbir2and ex syld imp
      ) AEFZBEFZGZCABUAHZFZDVQFZGZCDIHZUBUCBAIHZJKZVPVTCEFZACLKZCBJKZMZDEFZADLK
      ZDBJKZMZGZWCVOVNBUDFZVTWLUEBUFVNWMGZVTWLWNVRWGVSWKABCNABDNUGUHUIVPWLWCVPW
      LGZWCWBUJZWAJKZWAWBJKZWOWAEFZWBEFZWCWQWRGOWOWDWHWSWDWEWFWKVPUKZWHWIWJWGVP
      UMZCDPQZWOVOVNWTVNVOWLULZVNVOWLUNZBAPQZWAWBUOQWOWPABIHZWAJWOBRFARFWPXGUPW
      OBXDVCWOAXEVCBAUQQWOXGCBIHZWAWOVNVOXGEFXEXDABPQWOWDVOXHEFXAXDCBPQXCWOWEXG
      XHLKZWDWEWFWKVPURWOVNWDVOWEXIOXEXAXDACBUSSTWOWJXHWAJKZWHWIWJWGVPUTWOWHVOW
      DWJXJOXBXDXADBCVASTVBVDWOWABDIHZWBXCWOVOWHXKEFXDXBBDPQXFWOWFWAXKJKZWDWEWF
      WKVPVEWOWDVOWHWFXLOXAXDXBCBDVFSTWOWIXKWBLKZWHWIWJWGVPVGWOVNWHVOWIXMOXEXBX
      DADBVHSTVIVJVKVLVM $.
      $( [12-Sep-2014] $)

    $( Modular reduction produces a half-open interval.  (Contributed by Stefan
       O'Rear, 12-Sep-2014.) $)
    modelico $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) e. ( 0 [,) B ) ) $=
      ( cr wcel crp wa cmo co cc0 cico cle wbr clt cxr w3a wb 0re rpre rexr syl
      adantl elico2 sylancr modcl modge0 modlt mpbir3and ) ACDZBEDZFZABGHZIBJHD
      ZUKCDZIUKKLZUKBMLZUJICDBNDZULUMUNUOOPQUIUPUHUIBCDUPBRBSTUAIBUKUBUCABUDABU
      EABUFUG $.
      $( [12-Sep-2014] $)
  $}

