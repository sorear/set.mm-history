$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Miscellanea for Lagrange's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Two elements in a half-open interval have separation _strictly_ less
       than the difference between the endpoints.  (Contributed by Stefan
       O'Rear, 12-Sep-2014.) $)
    icodiamlt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. ( A [,) B ) /\ D e. (
        A [,) B ) ) ) -> ( abs ` ( C - D ) ) < ( B - A ) ) $=
      ( cr wcel wa co cmin clt wbr cle w3a elico2 cc syl2anc resubcl wb syl3anc
      mpbid cico cabs cfv cxr rexr anbi12d biimpd sylan2 cneg wceq simplr recnd
      wi simpll negsubdi2 simprl1 simprr1 simprl2 lesub1 simprr3 ltsub2 lelttrd
      eqbrtrd simprl3 ltsub1 simprr2 lesub2 ltletrd abslt mpbir2and ex syld imp
      ) AEFZBEFZGZCABUAHZFZDVQFZGZCDIHZUBUCBAIHZJKZVPVTCEFZACLKZCBJKZMZDEFZADLK
      ZDBJKZMZGZWCVOVNBUDFZVTWLUMBUEVNWMGZVTWLWNVRWGVSWKABCNABDNUFUGUHVPWLWCVPW
      LGZWCWBUIZWAJKZWAWBJKZWOWPABIHZWAJWOBOFAOFWPWSUJWOBVNVOWLUKZULWOAVNVOWLUN
      ZULBAUOPWOWSCBIHZWAWOVNVOWSEFXAWTABQPWOWDVOXBEFWDWEWFWKVPUPZWTCBQPWOWDWHW
      AEFZXCWHWIWJWGVPUQZCDQPZWOWEWSXBLKZWDWEWFWKVPURWOVNWDVOWEXGRXAXCWTACBUSST
      WOWJXBWAJKZWHWIWJWGVPUTWOWHVOWDWJXHRXEWTXCDBCVASTVBVCWOWABDIHZWBXFWOVOWHX
      IEFWTXEBDQPWOVOVNWBEFZWTXABAQPZWOWFWAXIJKZWDWEWFWKVPVDWOWDVOWHWFXLRXCWTXE
      CBDVESTWOWIXIWBLKZWHWIWJWGVPVFWOVNWHVOWIXMRXAXEWTADBVGSTVHWOXDXJWCWQWRGRX
      FXKWAWBVIPVJVKVLVM $.
      $( [12-Sep-2014] $)

    $( Modular reduction produces a half-open interval.  (Contributed by Stefan
       O'Rear, 12-Sep-2014.) $)
    modelico $p |- ( ( A e. RR /\ B e. RR+ ) -> ( A mod B ) e. ( 0 [,) B ) ) $=
      ( cr wcel crp wa cmo co cc0 cle wbr clt modcl modge0 modlt cxr w3a wb 0re
      cico rpre rexr syl adantl elico2 sylancr mpbir3and ) ACDZBEDZFZABGHZIBTHD
      ZUKCDZIUKJKZUKBLKZABMABNABOUJICDBPDZULUMUNUOQRSUIUPUHUIBCDUPBUABUBUCUDIBU
      KUEUFUG $.
      $( [12-Sep-2014] $)
  $}

