$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Sequences and sums
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f g h j k n w x y A $.  $d h j k m w x J $.  $d f g h j k m n w x y M $.
    $d g j ch $.  $d j m n w x F $.  $d f h j k x y ps $.  $d f g j n x y si $.
    $d f g h j k m n w x G $.  $d j n w x y ph $.  $d n w x y th $.  $d h V $.
    $d h j k n w x y ta $.  $d f g h j k m n w x y Z $.
    sdc.1 $e |- Z = ( ZZ>= ` M ) $.
    sdc.2 $e |- ( g = ( f |` ( M ... n ) ) -> ( ps <-> ch ) ) $.
    sdc.3 $e |- ( n = M -> ( ps <-> ta ) ) $.
    sdc.4 $e |- ( n = k -> ( ps <-> th ) ) $.
    sdc.5 $e |- ( ( g = h /\ n = ( k + 1 ) ) -> ( ps <-> si ) ) $.
    sdc.6 $e |- ( ph -> A e. V ) $.
    sdc.7 $e |- ( ph -> M e. ZZ ) $.
    sdc.8 $e |- ( ph -> E. g ( g : { M } --> A /\ ta ) ) $.
    sdc.9 $e |- ( ( ph /\ k e. Z ) -> ( ( g : ( M ... k ) --> A /\ th ) ->
                  E. h ( h : ( M ... ( k + 1 ) ) --> A /\
                         g = ( h |` ( M ... k ) ) /\ si ) ) ) $.
    ${
      sdc.10 $e |- J = { g | E. n e. Z ( g : ( M ... n ) --> A /\ ps ) } $.
      sdc.11 $e |- F = ( w e. Z , x e. J |-> { h | E. k e. Z
     ( h : ( M ... ( k + 1 ) ) --> A /\ x = ( h |` ( M ... k ) ) /\ si ) } ) $.
      ${
        sdc.12 $e |- ( ph -> A. k ph ) $.
        sdc.13 $e |- ( ph -> G : Z --> J ) $.
        sdc.14 $e |- ( ph -> ( G ` M ) : ( M ... M ) --> A ) $.
        sdc.15 $e |- ( ( ph /\ w e. Z ) ->
                       ( G ` ( w + 1 ) ) e. ( w F ( G ` w ) ) ) $.
        $( Lemma for ~ sdc . $)
        sdclem2 $p |- ( ph -> E. f ( f : Z --> A /\ A. n e. Z ch ) ) $=
          ( vm cv cfv cmpt wf cfz co wsbc wral wa wex wcel ffvelrn sylan eleq2i
          wrex cab ax-17 fvex hban wceq anbi12d rexbidv sylib cdm fdm adantr wi
          feq1 cuz c1 caddc fveq2 oveq2 mpteq1 syl eqeq12d imbi2d fveq12d fvmpt
          id eqid adantl fveq1d ex ralrimi wfn wb ffn cvv a1i mprg sylancl cres
          fnmpt w3a simpr wss cmap ovex mpan2 ssexg syl2anc eqeq1 reseq1 eqeq2d
          vex fzssp1 ax-mp syl6eleq fzopth mpbid simprd resmpt fvres syl5ibrcom
          eluzfz2 3syl sylbid mpd syl6eq bitrd sbcbidgv dfsbcq mptex hbsbc1v cz
          hbrex sbceq1a elabf bitri elfz1eq fveq2d eqtr2d eqfnfv2 mpbird 3simpa
          reximi ss2abi eqeltri simpl elmapg syl5ibr abssdv a1d sylancr 3anbi2d
          abrexex2g abbidv eleq1d imbi12d ovmpt4g 3com12 vtoclga eqsstrd sseldd
          3exp syl3c elab eqfnfv simprl fssres simprr fneq1d mpbiri fndm eqtr3d
          ad2antrl simplr oveq1d oveq2d wo reseq2d mp2b syl6req eqtrd syl5ibcom
          elfzp1 eqeltrrd peano2uz jaod ralrimiv mpbir2and expr expimpd rexlimd
          eqcomd imbi1d expcom sylbir uzind4 eleq2s impcom dmmptg eqeq1d syl6bi
          dmeqd syl5 feq2d equcoms biimpcd sylcom rexlimdvw simpld cbvmptv fmpt
          a2d cbvralv sylibr resex sbcie fzssuz sseqtr4i syl5bbr ralbidv cla4ev
          ) ATIUPTUPUQZUYLPURZURZUSZUTZBKUPRNUQZVAVBZUYNUSZVCZNTVDZTIJUQZUTZCNT
          VDZVEZJVFAMUQZVUFPURZURZIVGZMTVDUYPAVUIMTULAVUFTVGZVUIAVUJVEZRVUFVAVB
          ZIVUGUTZVUFVULVGZVUIVUKVUMDKVUGVCZVUKUYRIVUGUTZBKVUGVCZVEZNTVKZVUMVUO
          VEZVUKVUGQVGZVUSATQPUTZVUJVVAUMTQVUFPVHVIVVAVUGUYRIKUQZUTZBVEZNTVKZKV
          LZVGVUSQVVGVUGUJVJVVFVUSKVUGVURKNTUYQTVGKVMVUPVUQKVUPKVMBKVUGVUFPVNZU
          UAVOUUCVVHVVCVUGVPZVVEVURNTVVIVVDVUPBVUQUYRIVVCVUGWDBKVUGUUDVQVRUUEUU
          FVSVUKVURVUTNTVUKVURVUFUYQVPZVUTVURVUGVTZUYRVPZVUKVVJVUPVVLVUQUYRIVUG
          WAWBVUKVVLRRVPZVVJVEZVVJVUKVVLVULUYRVPZVVNVUKVVKVULUYRVUKVVKUPVULUYNU
          SZVTZVULVUKVUGVVPVUJAVUGVVPVPZAVVRWCZVUFRWEURZTAGUQZPURZUPRVWAVAVBZUY
          NUSZVPZWCARPURZUPRRVAVBZUYNUSZVPZWCZAHUQZPURZUPRVWKVAVBZUYNUSZVPZWCAV
          WKWFWGVBZPURZUPRVWPVAVBZUYNUSZVPZWCVVSGHRVUFVWARVPZVWEVWIAVXAVWBVWFVW
          DVWHVWARPWHVXAVWCVWGVPVWDVWHVPVWARRVAWIUPVWCVWGUYNWJWKWLWMVWAVWKVPZVW
          EVWOAVXBVWBVWLVWDVWNVWAVWKPWHVXBVWCVWMVPVWDVWNVPVWAVWKRVAWIUPVWCVWMUY
          NWJWKWLWMVWAVWPVPZVWEVWTAVXCVWBVWQVWDVWSVWAVWPPWHVXCVWCVWRVPVWDVWSVPV
          WAVWPRVAWIUPVWCVWRUYNWJWKWLWMVWAVUFVPZVWEVVRAVXDVWBVUGVWDVVPVWAVUFPWH
          VXDVWCVULVPVWDVVPVPVWAVUFRVAWIUPVWCVULUYNWJWKWLWMVWJRUUBVGAVWIVUFVWFU
          RZVUFVWHURZVPZMVWGVDZAVXGMVWGULAVUFVWGVGZVXGAVXIVEZVXFVUHVXEVXIVXFVUH
          VPAUPVUFUYNVUHVWGVWHUYLVUFVPZUYLVUFUYMVUGUYLVUFPWHVXKWPWNZVWHWQZVUFVU
          GVNWOWRVXJVUFVUGVWFVXJVUFRPVXIVUFRVPAVUFRUUGWRUUHWSUUIWTXAAVWFVWGXBZV
          WHVWGXBZVWIVXHXCAVWGIVWFUTVXNUNVWGIVWFXDWKUYNXEVGZVXOUPVWGUPVWGUYNVWH
          XEVXMXJVXPUYLVWGVGUYLUYMVNZXFXGMVWGVWFVWHUUJXHUUKXFVWKVVTVGZAVWOVWTVX
          RVWKTVGZAVWOVWTWCZWCTVVTVWKUAVJAVXSVXTAVXSVEZRVUFWFWGVBZVAVBZIVWQUTZV
          WLVWQVULXIZVPZVEZMTVKZVXTVYAVWQVYCILUQZUTZVWLVYIVULXIZVPZVEZMTVKZLVLZ
          VGVYHVYAVWKVWLOVBZVYOVWQVYAVYPVYJVYLFXKZMTVKZLVLZVYOVYAVWLQVGZVXSVYSX
          EVGZVYPVYSVPZAVVBVXSVYTUMTQVWKPVHVIAVXSXLVYAVYSVYOXMZVYOXEVGZWUAWUCVY
          AVYRVYNLVYQVYMMTVYJVYLFUULUUMUUNXFZVYATXEVGVYMLVLZXEVGZMTVDWUDTVVTXEU
          ARWEVNUUOZVYAWUGMTAVXSMULVXSMVMVOZVYAWUGVUJVYAWUFIVYCXNVBZXMZWUJXEVGW
          UGVYAISVGZWUKAWULVXSUFWBWULVYMLWUJVYMVYIWUJVGZWULVYJVYJVYLUUPWULVYCXE
          VGWUMVYJXCRVYBVAXOIVYCVYISXEUUQXPUURUUSWKIVYCXNXOWUFWUJXEXQXHUUTXAVYM
          MLTXEXEUVCUVAVYSVYOXEXQXRVXSVYJVWAVYKVPZFXKZMTVKZLVLZXEVGZVWKVWAOVBZW
          UQVPZWCZWCVXSWUAWUBWCZWCGVWLQVWAVWLVPZWVAWVBVXSWVCWURWUAWUTWUBWVCWUQV
          YSXEWVCWUPVYRLWVCWUOVYQMTWVCWUNVYLVYJFVWAVWLVYKXSUVBVRUVDZUVEWVCWUSVY
          PWUQVYSVWAVWLVWKOWIWVDWLUVFWMVWAQVGZVXSWURWUTVXSWVEWURWUTHGTQWUQOXEUK
          UVGUVHUVLUVIUVMWUEUVJUOUVKVYNVYHLVWQVWPPVNVYIVWQVPZVYMVYGMTWVFVYJVYDV
          YLVYFVYCIVYIVWQWDWVFVYKVYEVWLVYIVWQVULXTYAVQVRUVNVSVYAVYGVXTMTWUIVXTM
          VMVYAVUJVYGVXTWCVYAVUJVEZVYDVYFVXTWVGVYDVEVXTVYFVYEVWNVPZVWTWCWVGVYDW
          VHVWTWVGVYDWVHVEZVEZVWTVYCVWRVPZVWAVWQURZVWAVWSURZVPZGVYCVDZWVJVWQVYC
          XBZVWSVWRXBZVWTWVKWVOVEXCVYDWVPWVGWVHVYCIVWQXDUWCVXPWVQUPVWRUPVWRUYNV
          WSXEVWSWQZXJVXPUYLVWRVGVXQXFXGGVYCVWRVWQVWSUVOXHWVJVYBVWPRVAWVJVUFVWK
          WFWGWVJVVMVUFVWKVPZWVJVULVWMVPZVVMWVSVEZWVJVYEVTZVULVWMWVJVULIVYEUTZW
          WBVULVPWVJVYDVULVYCXMZWWCWVGVYDWVHUVPVUFXEVGWWDMYBRVUFXEYCYDVYCIVULVW
          QUVQXHVULIVYEWAWKWVJVYEVWMXBZWWBVWMVPWVJWWEVWNVWMXBZVXPWWFUPVWMUPVWMU
          YNVWNXEVWNWQXJVXPUYLVWMVGVXQXFXGWVJVWMVYEVWNWVGVYDWVHUVRZUVSUVTVWMVYE
          UWAWKUWBZWVJVUFVVTVGZVWKXEVGZWVTWWAXCWVJVUFTVVTVYAVUJWVIUWDUAYEZHYBZX
          ERVWKRVUFYFXHYGYHZUWEZUWFWVJWVNGVYCWVJVWAVYCVGZVWAVULVGZVWAVYBVPZUWGZ
          WVNWVJWWIWWOWWRXCWWKVWARVUFUWMWKWVJWWPWVNWWQWVJVWAVYEURZVWAVWSVULXIZU
          RZVPWWPWVNWVJVWAVYEWWTWVJVYEVWNWWTWWGWVJWWTVWSVWMXIZVWNWVJVULVWMVWSWW
          HUWHWWJVWMVWRXMWXBVWNVPWWLRVWKXEYCUPVWRVWMUYNYIUWIUWJUWKWSWWPWWSWVLWX
          AWVMVWAVULVWQYJVWAVULVWSYJWLUWLWVJWWQVXCWVNWVJVYBVWPVWAWWNYAWVJWVNVXC
          VWPVWQURZVWPVWSURZVPWVJWXDWXCWVJVWPVWRVGZWXDWXCVPWVJVXRVWPVVTVGWXEWVJ
          VUFVWKVVTWWMWWKUWNRVWKUWORVWPYLYMUPVWPUYNWXCVWRVWSUYLVWPVPZUYLVWPUYMV
          WQUYLVWPPWHWXFWPWNWVRVWPVWQVNWOWKUXBVXCWVLWXCWVMWXDVWAVWPVWQWHVWAVWPV
          WSWHWLYKYNUWPYNUWQUWRUWSVYFVWOWVHVWTVWLVYEVWNXSUXCYKUWTWTUXAYOUXDUXEU
          YBUXFUAUXGUXHZUXLVXPVVQVULVPUPVULUPVULUYNXEUXIVXPUYLVULVGVXQXFXGYPUXJ
          VUKWWIUYQXEVGVVOVVNXCVUKVUFTVVTAVUJXLUAYEZNYBXERUYQRVUFYFXHYQVVMVVJXL
          UXKUXMVVJVURVUTVURVUTXCNMUYQVUFVPZVUPVUMVUQVUOWXIUYRVULIVUGUYQVUFRVAW
          IZUXNWXIVUGXEVGVUQVUOXCVVHWXIBDKVUGXEUDYRXPVQUXOUXPUXQUXRYOZUXSVUKWWI
          VUNWXHRVUFYLWKVULIVUFVUGVHXRWTXAMTIVUHUYOUPMTUYNVUHVXLUXTUYAVSADKVVPV
          CZMTVDVUAAWXLMTULAVUJWXLVUKVUOWXLVUKVUMVUOWXKYHVUKVVRVUOWXLXCWXGDKVUG
          VVPYSWKYGWTXAUYTWXLNMTWXIUYTBKVVPVCZWXLWXIUYRVULVPUYSVVPVPUYTWXMXCWXJ
          UPUYRVULUYNWJBKUYSVVPYSYMWXIVVPXEVGWXMWXLXCUPVULUYNRVUFVAXOYTWXIBDKVV
          PXEUDYRXPYQUYCUYDVUEUYPVUAVEJUYOUPTUYNWUHYTVUBUYOVPZVUCUYPVUDVUATIVUB
          UYOWDWXNCUYTNTCBKVUBUYRXIZVCZWXNUYTBCKWXOVUBUYRJYBUYEUBUYFWXNWXOUYSVP
          WXPUYTXCWXNWXOUYOUYRXIZUYSVUBUYOUYRXTUYRTXMWXQUYSVPUYRVVTTRUYQUYGUAUY
          HUPTUYRUYNYIYDYPBKWXOUYSYSWKUYIUYJVQUYKXR $.
          $( [3-Jun-2014] $)
      $}

      $d g h k ph $.
      $( Lemma for ~ sdc . $)
      sdclem1 $p |- ( ph -> E. f ( f : Z --> A /\ A. n e. Z ch ) ) $=
        ( vj vm csn cv wf wa wex wral cz wcel cc0 cif cuz cfv wceq c1 caddc w3a
        co cvv cxp cpw c0 cdif cfz wrex cab fvex eqeltri cmap wss simpl wb ovex
        elmapg sylancl syl5ibr abssdv ssexg ralrimivw abrexex2g syl5eqel adantr
        sylancr uzid syl6eleqr simprl feq2d mpbird simprr oveq2 anbi12d rcla4ev
        syl fzsn syl12anc abeq2i sylibr cres wne wsbc peano2uzs ad2antlr simpr1
        simpr3 vex wi a1i sbc2iedv ad2antrr ax-17 hbsbc1v hbsb sbceq1a sbcbidgv
        mpan2 rcla4e rexbidv ex sylib fveq2d syl6eqr sylan ax17el hbre1 hbxfreq
        hban hbab exlimdv mpd eleq2i hbs1 hbrex feq1 elabf bitri elpw2g cbvrexv
        rexlimdva reximdva rexcom4 syl6ib syl5bi ss2abdv syl5eqss eqeq1 3anbi2d
        sselda exbidv elab abn0 adantlr eldifsn adantrl ralrimivva fmpt2 iftrue
        sylanbrc xpeq1d biimpar syldan 0z eqid axdc4uz syl3anc abbii eqtri feq3
        elimel ax-mp syl6bb eqeq1d raleqdv 3anbi123d simpll hbf cmpt2 hbov hbel
        hbmpt2 hbral hb3an simpr2 feq12d oveq1 id fveq2 oveq12d eleq12d sdclem2
        rcla4cva sylbid ) AQUMZIKUNZUOZEUPZKUQZSIJUNUOCNSURUPJUQZUGAUXFUXHKAUXF
        UXHAUXFUPZQUSUTZQVAVBZVCVDZPUKUNZUOZUXKUXMVDZUXDVEZULUNZVFVGVIZUXMVDZUX
        QUXQUXMVDZOVIZUTZULUXLURZVHZUKUQZUXHUXIPVJUTZUXDPUTZUXLPVKZPVLZVMUMVNZO
        UOZUYEAUYFUXFAPQNUNZVOVIZIUXDUOZBUPZNSVPZKVQZVJUIASVJUTUYOKVQZVJUTZNSUR
        UYQVJUTSQVCVDZVJTQVCVRVSAUYSNSAUYRIUYMVTVIZWAVUAVJUTUYSAUYOKVUAUYOUXDVU
        AUTZAUYNUYNBWBAIRUTZUYMVJUTVUBUYNWCUEQUYLVOWDIUYMUXDRVJWEWFWGWHIUYMVTWD
        UYRVUAVJWIWFWJUYONKSVJVJWKWNWLZWMUXIUYPUYGUXIQSUTQQVOVIZIUXDUOZEUYPUXIQ
        UYTSUXIUXJQUYTUTAUXJUXFUFWMZQWOXDTWPUXIVUFUXEAUXEEWQZUXIVUEUXCIUXDUXIUX
        JVUEUXCVEZVUGQXEZXDWRWSAUXEEWTUYOVUFEUPNQSUYLQVEZUYNVUFBEVUKUYMVUEIUXDU
        YLQQVOXAWRUBXBXCXFUYPKPUIXGXHAUXFSPVKZUYJOUOZUYKUXIQMUNZVFVGVIZVOVIZILU
        NZUOZGUNZVUQQVUNVOVIZXIZVEZFVHZMSVPZLVQZUYJUTZGPURHSURVUMUXIVVFHGSPUXIV
        USPUTZVVFHUNZSUTZUXIVVGUPZVVEUYIUTZVVEVMXJZVVFVVJVVKVVEPWAZAVVMUXFVVGAV
        VDLPAVVCVUQPUTZMSAVUNSUTZUPZVVCVVNVVPVVCUPZUYMIVUQUOZBKVUQXKZUPZNSVPZVV
        NVVQVUOSUTZVURBNVUOXKZKVUQXKZVWAVVOVWBAVVCQVUNSTXLXMVVPVURVVBFXNVVQVWDF
        VVPVURVVBFXOAVWDFWCVVOVVCABFKNVUQVUOLXPZVUNVFVGWDZUXDVUQVEZUYLVUOVEZUPB
        FWCXQAUDXRXSXTWSVVTVURVWDUPNVUOSVURVWDNVURNYAVWCKLNBNVUOVWFYBYCYQVWHVVR
        VURVVSVWDVWHUYMVUPIVUQUYLVUOQVOXAWRVWHVUQVJUTVVSVWDWCVWEVWHBVWCKVUQVJBN
        VUOYDYEYFXBYGXFVVNVUQUYQUTVWAPUYQVUQUIUUAUYPVWAKVUQVVTKNSUYLSUTZKYAVVRV
        VSKVVRKYABKLUUBYQUUCVWEVWGUYOVVTNSVWGUYNVVRBVVSUYMIUXDVUQUUDBKVUQYDXBYH
        UUEUUFXHYIUUIWHXTVVJUYFVVKVVMWCAUYFUXFVVGVUDXTVVEPVJUUGXDWSAVVGVVLUXFAV
        VGUPZVVDLUQZVVLVWJVUSVURUXDVVAVEZFVHZMSVPZLUQZKVQZUTVWKAPVWPVUSAPUYQVWP
        UIAUYPVWOKUYPVUTIUXDUOZDUPZMSVPZAVWOUYOVWRNMSUYLVUNVEZUYNVWQBDVWTUYMVUT
        IUXDUYLVUNQVOXAWRUCXBUUHZAVWSVWMLUQZMSVPVWOAVWRVXBMSUHUUJVWMMLSUUKUULUU
        MUUNUUOUURVWOVWKKVUSGXPUXDVUSVEZVWNVVDLVXCVWMVVCMSVXCVWLVVBVURFUXDVUSVV
        AUUPUUQYHUUSUUTYJVVDLUVAXHUVBVVEUYIVMUVCUVHUVDUVEHGSPVVEUYJOUJUVFYJAUYK
        VUMAUYHVULUYJOAUXLSPAUXLUYTSAUXKQVCAUXJUXKQVEZUFUXJQVAUVGZXDYKTYLUVIWRU
        VJUVKPUXDUKULOUXKVJUXLQVAUSUVLUVSUXLUVMUVNUVOUXIUYDUXHUKUXIUYDSVWSKVQZU
        XMUOZQUXMVDZUXDVEZUYBULSURZVHZUXHUXIUXNVXGUXPVXIUYCVXJUXIUXNSPUXMUOZVXG
        UXIUXLSPUXMUXIUXLUYTSUXIUXKQVCUXIUXJVXDVUGVXEXDZYKTYLZWRPVXFVEVXLVXGWCP
        UYQVXFUIUYPVWSKVXAUVPUVQZPVXFSUXMUVRUVTZUWAUXIUXOVXHUXDUXIUXKQUXMVXMYKU
        WBUXIUYBULUXLSVXNUWCUWDUXIVXKUXHUXIVXKUPZBCDEFGHIJKLMNOUXMPQRSTUAUBUCUD
        AVUCUXFVXKUEXTAUXJUXFVXKUFXTZAUXGUXFVXKUGXTVXQAVVOVWRVXBXQAUXFVXKUWEUHY
        MUIUJUXIVXKMUXIMYAVXGVXIVXJMMNSVXFUXMNUKMYNVWIMYAZVWSMKNVWRMSYOYRZUWFVX
        IMYAUYBMULSUXQSUTMYAMNNUXSUYAUYLUXSUTMYAMNUXQUXTONULMYNMNOHGSPVVEUWGUJH
        GMNSPVVEVXSMNPVXFVXOVXTYPVVDMLNVVCMSYOYRUWJYPUYLUXTUTMYAUWHUWIUWKUWLYQV
        XQVXGVXLUXIVXGVXIVXJXNVXPXHVXQVUEIVXHUOUXEUXIUXEVXKVUHWMVXQVUEUXCIVXHUX
        DUXIVXGVXIVXJUWMVXQUXJVUIVXRVUJXDUWNWSVXQVXJVVIVVHVFVGVIZUXMVDZVVHVVHUX
        MVDZOVIZUTZUXIVXGVXIVXJXOUYBVYEULVVHSUXQVVHVEZUXSVYBUYAVYDVYFUXRVYAUXMU
        XQVVHVFVGUWOYKVYFUXQVVHUXTVYCOVYFUWPUXQVVHUXMUWQUWRUWSUXAYMUWTYIUXBYSYT
        YIYSYT $.
        $( [3-Jun-2014] $)
    $}

    $d g h k ph $.
    $( Strong dependent choice.  Suppose we may choose an element of ` A ` such
       that property ` ps ` holds, and suppose that if we have already chosen
       the first ` k ` elements (represented here by a function from
       ` 1 ... k ` to ` A ` ), we may choose another element so that all
       ` k + 1 ` elements taken together have property ` ps ` .  Then there
       exists an infinite sequence of elements of ` A ` such that the first
       ` n ` terms of this sequence satisfy ` ps ` for all ` n ` .  This
       theorem allows us to construct infinite seqeunces where each term
       depends on all the previous terms in the sequence.  (Contributed by Jeff
       Madsen, 2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       3-Jun-2014.) $)
    sdc $p |- ( ph -> E. f ( f : Z --> A /\ A. n e. Z ch ) ) $=
      ( vx vy vj cv cfz co wf wa wrex cab caddc cres wceq w3a cmpt2 oveq2 feq2d
      eqid anbi12d cbvrexv abbii mpt2eq123i eqidd eqeq1 3anbi2d abbidv cbvmpt2v
      c1 rexbidv eqtr3i sdclem1 ) ABCDEFUEUFGHIJKLUGHOMKUHZUIUJZGIUHZUKZDULZKOU
      MZIUNZMVPVLUOUJUIUJGJUHZUKZHUHZWCVQUPZUQZFURZKOUMZJUNZUSZMLUHZUIUJZGVRUKZ
      BULZLOUMZIUNZMNOPQRSTUAUBUCUDWQVBUGHOWQWJUSWKUFUEOWQWDUEUHZWFUQZFURZKOUMZ
      JUNZUSUGHOWQWJOWBWJOVBWPWAIWOVTLKOWLVPUQZWNVSBDXCWMVQGVRWLVPMUIUTVASVCVDV
      EWJVBVFUGHUFUEOWQWJXBWJUGUHUFUHUQWJVGWEWRUQZWIXAJXDWHWTKOXDWGWSWDFWEWRWFV
      HVIVMVJVKVNVO $.
      $( [3-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d C c f n $.  $d A a b c d f g m n x y $.  $d M a b c d f g j k m n x y $.
    $d Z a b c d g j m n x $.  $d N a b c d f g j k m n $.  $d R a b d $.
    $d ph c d f g j k m $.  $d ps a $.  $d ch a b c d g m n $.
    $d th c d f g n $.  $d ta a b c d g m $.  $d et a b g m $.
    fdc.1 $e |- A e. _V $.
    fdc.2 $e |- M e. ZZ $.
    fdc.3 $e |- Z = ( ZZ>= ` M ) $.
    fdc.4 $e |- N = ( M + 1 ) $.
    fdc.5 $e |- ( a = ( f ` ( k - 1 ) ) -> ( ph <-> ps ) ) $.
    fdc.6 $e |- ( b = ( f ` k ) -> ( ps <-> ch ) ) $.
    fdc.7 $e |- ( a = ( f ` n ) -> ( th <-> ta ) ) $.
    fdc.8 $e |- ( et -> C e. A ) $.
    fdc.9 $e |- ( et -> R Fr A ) $.
    fdc.10 $e |- ( ( et /\ a e. A ) -> ( th \/ E. b e. A ph ) ) $.
    fdc.11 $e |- ( ( ( et /\ ph ) /\ ( a e. A /\ b e. A ) ) -> b R a ) $.
    $( Finite version of dependent choice.  Construct a function whose value
       depends on the previous function value, except at a final point at which
       no new value can be chosen.  The final hypothesis ensures that the
       process will terminate.  The proof does not use the Axiom of Choice.
       (Contributed by Jeff Madsen, 18-Jun-2010.) $)
    fdc $p |- ( et -> E. n e. Z E. f ( f : ( M ... n ) --> A /\
              ( ( f ` M ) = C /\ ta ) /\ A. k e. ( N ... n ) ch ) ) $=
      ( vc vd vm vg vx vy vj wcel cv cfz co wf cfv wceq wa wral w3a wex wrex c0
      wss wbr wn wi wo wsbc cuz cz ax-mp csn sylancr adantr a1i fveq1 eqeq1d wb
      feq1 syl6eq anbi12d oveq2 feq2d fvex sbcie fveq2 dfsbcq syl5bbr anbi2d c1
      syl caddc oveq1i clt ltp1i peano2z mp2an raleqdv 3anbi123d exbidv rcla4ev
      mpbi adantll eqeq2 3anbi2d rexbidv ad2antll cmin cvv sbcbidgv mpan2 bitri
      anbi1d bitrd ad2antlr cif eleq2s cle ltnlei elfzle1 adantl iffalse ex imp
      eleq1 cc ax-1cn sylib fvopab4 eqeq1 oveq1 fveq2d ifbieq2d ifex crab elexi
      cdif uzid eleqtrri cop eqid vex fsn mpbir snssi fss fzsn feq2i fvsn simpr
      sylibr snex sbceq1a eqcoms bicomd cla4ev syl12anc zrei fzn eqtri mpbiran2
      df-3an ral0 syl6bb breq1 expcom dfrex2 syl6ib con2d eldif simplbi2 ssrab2
      dfss4 eleq2i elrab3 syl5bb sylibd cbvrexv ralbidv cbvexv rexbii peano2uzs
      a1d sbequ12r imbi1d imbi12d imbi2d copab peano2uz elfzp12 iftrue biimprcd
      eleq1d ad2antrr 1re readdcli syl6bi com12 mtoi elfzelz eluzelz 1z fzsubel
      biimpd mpanr2 mpanl1 3syl com23 mpd recni sylancl oveq12d eleqtrd ffvelrn
      pncan sylan2 anassrs ancom1s eqeltrd jaod sylbid ralrimiv fopab2 adantlll
      zcn 3ad2antr1 eluzfz1 eluzfz2 wne cr zre zleltp1 mpbid ltne syl3anc df-ne
      eluzle 3eqtrd biimpar ad2ant2l 3ad2antr2 eluzp1p1 fveq2i syl6eleqr biimpa
      adantlr eqtrd eqeq2i sylbi eluz2 mpbir3an peano2zdi fzss1 fzaddel mpanr12
      ltleii sseldd ltneii simprl biimparc adantlrr eqeltri sylan fzssp1 subadd
      rcla4va breqtrri mp3an23 eqcom addcomi syl5bi sselda lttri an32s adantlrl
      syldan jaodan ralrimiva cbvralv adantllr adantrlr 3adantr1 ovex syl121anc
      opabex2 chvarv syl2anc exlimdv rexlimdva 3syld an42s mpdan sylibrd notbii
      expr iman bitr4i syl6ibr nrexdv wfr difss difexg fri syl5bir ssdif0 eqssd
      mt3d rabid2 ) FHGUPMLUQZURUSZGJUQZUTZMVXQVAZUIUQZVBZEVCZCKNVXOURUSZVDZVEZ
      JVFZLOVGZUIGVDZVXRVXSHVBZEVCZVYDVEZJVFZLOVGZUEFGVYGUIGUUAZVBVYHFGVYNFGVYN
      UUCZVHVBZGVYNVIFVYPUJUQZPUQZIVJZVKUJVYOVDZPVYOVGZFVYTPVYOFVYRVYOUPZVYTVKZ
      FVYTWUBFVYTVYRGUPZVYRVYNUPZVLZWUBVKZFWUDVYTWUEFWUDVYTWUEVLFWUDVCZVYTVXRVX
      SVYRVBZEVCZVYDVEZJVFZLOVGZWUEWUHDAQGVGZVMVYTWUMVLZUGWUHDWUOWUNWUHDVCWUMVY
      TWUDDWUMFWUDDVCZMOUPMMURUSZGVXQUTZWUIDPVXSVNZVCZVCZJVFZWUMMMVOVAZOMVPUPZM
      WVCUPSMUUDVQTUUEWUPWUQGMVYRUUFZVRZUTZMWVFVAZVYRVBZDWVBWUDWVGDWUDMVRZGWVFU
      TZWVGWUDWVJVYRVRZWVFUTZWVLGVIWVKWVMWVFWVFVBWVFUUGMVYRWVFMVPSUUBZPUUHZUUIU
      UJVYRGUUKWVJWVLGWVFUULVSWUQWVJGWVFWVDWUQWVJVBSMUUMVQUUNUUQVTWVIWUPMVYRWVN
      WVOUUOZWAWUDDUUPWVAWVGWVIDVCZVCJWVFWVEUURVXQWVFVBZWURWVGWUTWVQWUQGVXQWVFW
      EWVRWUIWVIWUSDWVRVXSWVHVYRMVXQWVFWBZWCWVRWUIWUSDWDWVRVXSWVHVYRWVSWVPWFWUI
      DWUSDWUSWDVYRVXSDPVXSUUSUUTUVAWQWGWGUVBUVCWULWVBLMOVXOMVBZWUKWVAJWVTWUKWU
      RWUTCKVHVDZVEZWVAWVTVXRWURWUJWUTVYDWWAWVTVXPWUQGVXQVXOMMURWHWIWVTEWUSWUIE
      DPVXOVXQVAZVNZWVTWUSDEPWWCVXOVXQWJUDWKZWVTWWCVXSVBWWDWUSWDVXOMVXQWLDPWWCV
      XSWMWQWNWOWVTCKVYCVHWVTVYCNMURUSZVHVXOMNURWHWWFMWPWRUSZMURUSZVHNWWGMURUAW
      SMWWGWTVJZWWHVHVBZMMSUVDZXAZWWGVPUPZWVDWWIWWJWDWVDWWMSMXBVQZSWWGMUVEXCXHU
      VFWFXDXEWWBWVAWWAWURWUTWWAUVHCKUVIUVGUVJXFXGVSXIUWIWUHWUNWUOWUHAWUOQGWUHQ
      UQZGUPZAWUOFAWUDWWPWUOFAVCZWUDWWPVCZVCZVYTWWOVYOUPZVKZVXRVXSWWOVBZEVCZVYD
      VEZJVFZLOVGZWUMWWSWWTVYTWWSWWTVYSUJVYOVGZWUCWWSWWOVYRIVJZWWTWXGVLUHWWTWXH
      WXGVYSWXHUJWWOVYOVYQWWOVYRIUVKXGUVLWQVYSUJVYOUVMUVNUVOWWPWXAWXFVLWWQWUDWW
      PWXAWWOGVYOUUCZUPZWXFWXJWWPWXAWWOGVYOUVPUVQWXJWWOVYNUPWWPWXFWXIVYNWWOVYNG
      VIZWXIVYNVBVYGUIGUVRZVYNGUVSXHUVTVYGWXFUIWWOGVXTWWOVBZVYFWXELOWXMVYEWXDJW
      XMVYBWXCVXRVYDWXMVYAWXBEVXTWWOVXSXJXSXKXFXLUWAUWBUWCXMWXFMUKUQZURUSZGULUQ
      ZUTZMWXPVAZWWOVBZDPWXNWXPVAZVNZVCZAQKUQZWXPVAZVNZPWYCWPXNUSZWXPVAZVNZKNWX
      NURUSZVDZVEZULVFZUKOVGZWWSWUMWXFWXOGVXQUTZWXBDPWXNVXQVAZVNZVCZCKWYIVDZVEZ
      JVFZUKOVGWYMWXEWYTLUKOVXOWXNVBZWXDWYSJXUAVXRWYNWXCWYQVYDWYRXUAVXPWXOGVXQV
      XOWXNMURWHWIXUAEWYPWXBEWWDXUAWYPWWEXUAWWCWYOVBWWDWYPWDVXOWXNVXQWLDPWWCWYO
      WMWQWNWOXUACKVYCWYIVXOWXNNURWHXDXEXFUWDWYTWYLUKOWYSWYKJULVXQWXPVBZWYNWXQW
      YQWYBWYRWYJWXOGVXQWXPWEXUBWXBWXSWYPWYAXUBVXSWXRWWOMVXQWXPWBWCXUBWYOWXTVBW
      YPWYAWDWXNVXQWXPWBDPWYOWXTWMWQWGXUBCWYHKWYICAQWYCVXQVAZVNZPWYFVXQVAZVNZXU
      BWYHXUFBQXUCVNZCXUDXUGPXUEWYFVXQWJVYRXUEVBZXUCXOUPXUDXUGWDWYCVXQWJZXUHABQ
      XUCXOUBXPXQWKBCQXUCXUIUCWKXRZXUBXUFXUDPWYGVNZWYHXUBXUEWYGVBXUFXUKWDWYFVXQ
      WXPWBXUDPXUEWYGWMWQXUBWYGXOUPXUKWYHWDWYFWXPWJXUBXUDWYEPWYGXOXUBXUCWYDVBXU
      DWYEWDWYCVXQWXPWBAQXUCWYDWMWQXPXQXTWNUWEXEUWFUWGXRWWSWYLWUMUKOWWSWXNOUPZV
      CZWYKWUMULXUMWYKWUMXUMWYKVCWXNWPWRUSZOUPZMXUNURUSZGVXQUTZWUIDPXUNVXQVAZVN
      ZVCZCKNXUNURUSZVDZVEZJVFZWUMXULXUOWWSWYKMWXNOTUWHYAXUMWYKXVDAWWRXULWYKXVD
      VLZFAWUDXULXVEWWPAQVYQVNZWUDVCZXULVCZWXQWXRVYQVBZWYAVCZWYJVEZXVDVLZVLZAWU
      DVCZXULVCZXVEVLUJQVYQWWOVBZXVHXVOXVLXVEXVPXVGXVNXULXVPXVFAWUDAUJQUWJXSXSX
      VPXVKWYKXVDXVPXVJWYBWXQWYJXVPXVIWXSWYAVYQWWOWXRXJXSXKUWKUWLXVFPVXTVNZVXTG
      UPZVCZXULVCZXVKXUQVYAXUSVCZXVBVEZJVFZVLZVLXVMUIPVXTVYRVBZXVTXVHXWDXVLXWEX
      VSXVGXULXWEXVQXVFXVRWUDXVFUIPUWJVXTVYRGYKWGXSXWEXWCXVDXVKXWEXWBXVCJXWEXWA
      XUTXUQXVBXWEVYAWUIXUSVXTVYRVXSXJZXSXKXFUWMUWLXVTXVKXWCXVTXVKVCXUPGUMUQZXU
      PUPZUNUQXWGMVBZVXTXWGWPXNUSZWXPVAZYBZVBVCUMUNUWNZUTZMXWMVAZVXTVBZDPXUNXWM
      VAZVNZAQWYCXWMVAZVNZPWYFXWMVAZVNZKXVAVDZXWCXVTXVJWXQXWNWYJXVRXULWXQXWNXVQ
      XVRXULVCWXQVCZXWLGUPZUMXUPVDXWNXXDXXEUMXUPXXDXWHXWIXWGWWGXUNURUSZUPZVMZXX
      EXULXWHXXHWDZXVRWXQXULXUNWVCUPZXXIXXJWXNWVCOMWXNUWOZTYCXWGMXUNUWPWQYAXXDX
      WIXXEXXGXVRXWIXXEVLXULWXQXWIXXEXVRXWIXWLVXTGXWIVXTXWKUWQZUWSUWRUWTXULWXQX
      XGXXEVLXVRXULWXQVCZXXGXXEXXMXXGVCZXWLXWKGXXNXWIVKZXWLXWKVBXXGXXOXXMXXGXWI
      WWGMYDVJZWWIXXPVKWWLMWWGWWKMWPWWKUXAUXBZYEXHXWIXXGXXPXWIXXGMXXFUPXXPXWGMX
      XFYKMWWGXUNYFUXCUXDUXEYGXWIVXTXWKYHWQWXQXULXXGXWKGUPZWXQXULXXGXXRXULXXGVC
      ZWXQXWJWXOUPXXRXXSXWJWWGWPXNUSZXUNWPXNUSZURUSZWXOXXSXWGVPUPZXWJXYBUPZXXGX
      YCXULXWGWWGXUNUXFYGXULXXGXYCXYDVLXULXYCXXGXYDXULWXNVPUPZXUNVPUPZXYCXXGXYD
      VLZVLXYEWXNWVCOMWXNUXGTYCZWXNXBZXYFXYCXYGWWMXYFXYCXYGWWNWWMXYFVCZXYCWPVPU
      PZXYGUXHXYJXYCXYKVCVCXXGXYDXWGWPWWGXUNUXIUXJUXKUXLYIUXMUXNYJUXOXULXYBWXOV
      BXXGXULXXTMXYAWXNURXXTMVBZXULMYLUPZWPYLUPZXYLMWWKUXPZYMMWPUYAXCZWAXULWXNY
      LUPZXYNXYAWXNVBXULXYEXYQXYHWXNUYKWQYMWXNWPUYAUXQZUXRVTUXSWXOGXWJWXPUXTUYB
      UYCUYDUYEYIXIUYFUYGUYHUMUNXUPGXWLXWMXWMUUGZUYIYNUYJUYLXULXWPXVSXVKXULMXUP
      UPZXWPXYTWXNWVCOWXNWVCUPZXXJXYTXXKMXUNUYMWQTYCUMUNMXWLVXTXUPXWMXXLXYSUIUU
      HZYOWQZYAXVTWXQXVJXWRWYJXULWYAXWRXVSXVIXULXWRWYAXULXWQWXTVBXWRWYAWDXULXWQ
      XUNMVBZVXTXYAWXPVAZYBZYUEWXTXULXUNXUPUPZXWQYUFVBYUGWXNWVCOYUAXXJYUGXXKMXU
      NUYNWQTYCUMUNXUNXWLYUFXUPXWMXWGXUNVBZXWIYUDXWKYUEVXTXWGXUNMYPYUHXWJXYAWXP
      XWGXUNWPXNYQYRYSXYSYUDVXTYUEYUBXYAWXPWJYTYOWQXULYUDVKZYUFYUEVBXULXUNMUYOZ
      YUIXULMUYPUPZXUNUYPUPZMXUNWTVJZYUJYUKXULWWKWAXULXYEXYFYULXYHXYIXUNUYQUXMX
      ULMWXNYDVJZYUMYUNWXNWVCOMWXNVUCTYCXULWVDXYEYUNYUMWDSXYHMWXNUYRVSUYSMXUNUY
      TVUAXUNMVUBYNYUDVXTYUEYHWQXULXYAWXNWXPXYRYRVUDDPXWQWXTWMWQVUEVUFVUGXVTXVJ
      WYJXXCWXQXVTXVIWYJXXCWYAXVQXULXVIWYJVCZXXCXVRXVQXULVCZYUOVCZAQUOUQZXWMVAZ
      VNZPYURWPXNUSZXWMVAZVNZUOXVAVDXXCYUQYVCUOXVAYUQYURXVAUPZYURNVBZYURNWPWRUS
      ZXUNURUSZUPZVMZYVCYUPYVDYVIYUOXULYVDYVIXVQXULYVDYVIXULXUNNVOVAZUPYVDYVIWD
      XULXUNWWGVOVAZYVJXUNYVKUPWXNWVCOMWXNVUHTYCNWWGVOUAVUIVUJYURNXUNUWPWQVUKXI
      VULYUQYVEYVCYVHYUPXVIYVEYVCWYJYUPXVIYVEYVCXVQXULXVIYVEVCZYVCXULYVLVCZYVCX
      VQYVMYVCYUTPVXTVNZXVQYVMYVBVXTVBYVCYVNWDYVMYVBXWOVXTYVEYVBXWOVBXULXVIYVEY
      VAMXWMYVEYVANWPXNUSZMYURNWPXNYQYVOXXTMNWWGWPXNUAWSXYPUVFWFYRXMXULXWPYVLYU
      CVTVUMYUTPYVBVXTWMWQYVMVXTXOUPYVNXVQWDYUBYVMYUTXVFPVXTXOYVMYUSVYQVBYUTXVF
      WDYVMYUSWWGXWMVAZWXRVYQYVEYUSYVPVBZXULXVIYVEYURWWGVBZYVQNWWGYURUAVUNYURWW
      GXWMWLVUOXMXULYVPWXRVBYVLXULYVPWWGMVBZVXTWXRYBZWXRXULWWGXUPUPYVPYVTVBXULX
      XFXUPWWGXULWWGWVCUPZXYFXXFXUPVIYWAWVDWWMMWWGYDVJMWWGVUPSWWNMWWGWWKXXQWWLV
      VBVUQZXULWXNXYHVURZWWGMXUNVPVUSVSXULMWXOUPZWWGXXFUPZYWDWXNWVCOMWXNUYMTYCX
      ULWVDXYEYWDYWEWDZSXYHWVDXYEVCWVDXYKYWFSUXHMWPMWXNVUTVVAVSUYSVVCUMUNWWGXWL
      YVTXUPXWMXWGWWGVBZXWIYVSXWKWXRVXTXWGWWGMYPYWGXWJMWXPYWGXWJXXTMXWGWWGWPXNY
      QXYPWFYRYSXYSYVSVXTWXRYUBMWXPWJYTYOWQYVSVKZYVTWXRVBWWGMUYOYWHMWWGWWKXXQWW
      LVVDWWGMVUBXHYVSVXTWXRYHVQWFVTXULXVIYVEVVEVUDAQYUSVYQWMWQXPXQXTVVFUYCUYCV
      VGXULYUOYVHYVCXVQXULWYJYVHYVCXVIXULYVHWYJYVCXULYVHVCZWYJAQYVAWXPVAZVNZPYV
      AWPXNUSZWXPVAZVNZYVCYWIYVAWYIUPWYJYWNYWIYVAYVFWPXNUSZXYAURUSZWYIYWIYURVPU
      PZYVAYWPUPZYVHYWQXULYURYVFXUNUXFZYGXULYVHYWQYWRVLXULYWQYVHYWRXULYVFVPUPZX
      YFYWQYVHYWRVLZVLNVPUPYWTNWWGVPUAWWNVVHZNXBVQYWCYWTXYFVCZYWQYXAYXCYWQXYKYX
      AUXHYXCYWQXYKVCVCYVHYWRYURWPYVFXUNUXIUXJUXKYIVSUXNYJUXOXULYWPWYIVBYVHXULY
      WONXYAWXNURYWONVBZXULNYLUPXYNYXDNNYXBUVDZUXPYMNWPUYAXCWAXYRUXRVTUXSZWYHYW
      NKYVAWYIWYCYVAVBZWYHWYEPYWMVNZYWNYXGWYGYWMVBWYHYXHWDYXGWYFYWLWXPWYCYVAWPX
      NYQYRWYEPWYGYWMWMWQYXGYWMXOUPZYXHYWNWDYWLWXPWJZYXGWYEYWKPYWMXOYXGWYDYWJVB
      WYEYWKWDWYCYVAWXPWLAQWYDYWJWMWQXPXQXTVVLVVIYWIYVCYWNYWIYVCYUTPYWMVNZYWNYW
      IYVBYWMVBYVCYXKWDYWIYVBYVAMVBZVXTYWMYBZYWMYWIYVAXUPUPYVBYXMVBYWIXVAXUPYVA
      XULXVAXUPVIZYVHXULNWVCUPZXYFYXNNWWGWVCUAYWBVVHZYWCNMXUNVPVUSVSVTYWIWYIXVA
      YVAXULWYIXVAVIYVHNWXNOVVJVTYXFVVCVVCUMUNYVAXWLYXMXUPXWMXWGYVAVBZXWIYXLXWK
      YWMVXTXWGYVAMYPYXQXWJYWLWXPXWGYVAWPXNYQYRYSXYSYXLVXTYWMYUBYXJYTYOWQYWIYXL
      VKZYXMYWMVBYVHYXRXULYVHYXLYVFWWGYDVJZWWGYVFWTVJYXSVKWWGWWGWPWRUSYVFWTWWGX
      XQXANWWGWPWRUAWSVVMWWGYVFXXQNWPYXEUXAUXBZYEXHYVHYXLWPMWRUSZYURVBZYXSYVHYW
      QYURYLUPZYXLYYBWDZYWSYURUYKYYCXYNXYMYYDYMXYOYURWPMVVKVVNUXMYYBYVRYVHYXSYY
      BYURYYAVBYVRYYAYURVVOYYAWWGYURWPMYMXYOVVPVUNXRYVRYVHYXSYVRYVHWWGYVGUPYXSY
      URWWGYVGYKWWGYVFXUNYFUXCUXDVVQUYGUXEYGYXLVXTYWMYHWQVUMYUTPYVBYWMWMWQYWIYX
      IYXKYWNWDYXJYWIYUTYWKPYWMXOYWIYUSYWJVBYUTYWKWDYWIYUSYURMVBZVXTYWJYBZYWJYW
      IYURXUPUPYUSYYFVBXULYVGXUPYURXULYVFWVCUPZXYFYVGXUPVIYXOYYGYXPMNUWOVQYWCYV
      FMXUNVPVUSVSVVRUMUNYURXWLYYFXUPXWMXWGYURVBZXWIYYEXWKYWJVXTXWGYURMYPYYHXWJ
      YVAWXPXWGYURWPXNYQYRYSXYSYYEVXTYWJYUBYVAWXPWJYTYOWQYWIYYEVKZYYFYWJVBYVHYY
      IXULYVHYYEYVFMYDVJZMYVFWTVJZYYJVKMNWTVJNYVFWTVJYYKMWWGNWTWWLUAVVMNYXEXAMN
      YVFWWKYXEYXTVVSXCMYVFWWKYXTYEXHYYEYVHYYJYYEYVHMYVGUPYYJYURMYVGYKMYVFXUNYF
      UXCUXDUXEYGYYEVXTYWJYHWQVUMAQYUSYWJWMWQXPXQXTVUEVWBVVTVWAUYJVWCVWBVWDYVCX
      XBUOKXVAYURWYCVBZYVCYUTPXXAVNZXXBYYLYVBXXAVBYVCYYMWDYYLYVAWYFXWMYURWYCWPX
      NYQYRYUTPYVBXXAWMWQYYLXXAXOUPZYYMXXBWDWYFXWMWJZYYLYUTXWTPXXAXOYYLYUSXWSVB
      YUTXWTWDYURWYCXWMWLAQYUSXWSWMWQXPXQXTVWEYNVWFVWGVWHXWBXWNXWPXWRVCZXXCVEJX
      WMUMUNXUPXWLMXUNURVWIVWKVXQXWMVBZXUQXWNXWAYYPXVBXXCXUPGVXQXWMWEYYQVYAXWPX
      USXWRYYQVXSXWOVXTMVXQXWMWBWCYYQXURXWQVBXUSXWRWDXUNVXQXWMWBDPXURXWQWMWQWGY
      YQCXXBKXVACXUFYYQXXBXUJYYQXUFXUDPXXAVNZXXBYYQXUEXXAVBXUFYYRWDWYFVXQXWMWBX
      UDPXUEXXAWMWQYYQYYNYYRXXBWDYYOYYQXUDXWTPXXAXOYYQXUCXWSVBXUDXWTWDWYCVXQXWM
      WBAQXUCXWSWMWQXPXQXTWNUWEXEUVBVWJYIVWLVWLVVGUYJYJWULXVDLXUNOVXOXUNVBZWUKX
      VCJYYSVXRXUQWUJXUTVYDXVBYYSVXPXUPGVXQVXOXUNMURWHWIYYSEXUSWUIEWWDYYSXUSWWE
      YYSWWCXURVBWWDXUSWDVXOXUNVXQWLDPWWCXURWMWQWNWOYYSCKVYCXVAVXOXUNNURWHXDXEX
      FXGVWMYIVWNVWOVVQVWPVWQVXAVWOYJVWCVWRWUDWUEWUMWDFVYGWUMUIVYRGXWEVYFWULLOX
      WEVYEWUKJXWEVYBWUJVXRVYDXWEVYAWUIEXWFXSXKXFXLUWAYGVWSYIUXNWUGWUDWUEVKVCZV
      KWUFWUBYYTVYRGVYNUVPVWTWUDWUEVXBVXCVXDUVOYJVXEVYPVKVYOVHUYOZFWUAVYOVHVUBF
      GIVXFZVYOGVIZUUUAWUAVLUFGVYNVXGUUUBUUUCUUUAWUAVYOXOUPZUUUBUUUCUUUAVCWUAGX
      OUPUUUDRGVYNXOVXHVQPUJGVYOXOIVXIUXLVXAUXQVXJVXMGVYNVXKUUQWXKFWXLWAVXLVYGU
      IGVXNYNVYGVYMUIHGVXTHVBZVYFVYLLOUUUEVYEVYKJUUUEVYBVYJVXRVYDUUUEVYAVYIEVXT
      HVXSXJXSXKXFXLVVLVWM $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d A a b c d f n $.  $d R a b d $.  $d M a b c d f k n $.
    $d Z a b c d n $.  $d N a b c d f k n $.  $d ph d f k $.  $d ps a d $.
    $d ch a b c d n $.  $d th d f n $.  $d ta a b c d $.  $d et a b c d f n $.
    $d ze b c d f n $.  $d si a c $.
    fdc1.1 $e |- A e. _V $.
    fdc1.2 $e |- M e. ZZ $.
    fdc1.3 $e |- Z = ( ZZ>= ` M ) $.
    fdc1.4 $e |- N = ( M + 1 ) $.
    fdc1.5 $e |- ( a = ( f ` M ) -> ( ze <-> si ) ) $.
    fdc1.6 $e |- ( a = ( f ` ( k - 1 ) ) -> ( ph <-> ps ) ) $.
    fdc1.7 $e |- ( b = ( f ` k ) -> ( ps <-> ch ) ) $.
    fdc1.8 $e |- ( a = ( f ` n ) -> ( th <-> ta ) ) $.
    fdc1.9 $e |- ( et -> E. a e. A ze ) $.
    fdc1.10 $e |- ( et -> R Fr A ) $.
    fdc1.11 $e |- ( ( et /\ a e. A ) -> ( th \/ E. b e. A ph ) ) $.
    fdc1.12 $e |- ( ( ( et /\ ph ) /\ ( a e. A /\ b e. A ) ) -> b R a ) $.
    $( Variant of ~ fdc with no specified base value.  (Contributed by Jeff
       Madsen, 18-Jun-2010.) $)
    fdc1 $p |- ( et -> E. n e. Z E. f ( f : ( M ... n ) --> A /\
                          ( si /\ ta ) /\ A. k e. ( N ... n ) ch ) ) $=
      ( vc vd wrex cv cfz co wf wa wral w3a wex wcel wsbc eleq1 anbi2d sbequ12r
      wi wceq anbi12d imbi1d cfv c1 cmin ax-17 sbhypf simprl wfr adantr wo hbs1
      hbrex hbor hbim sbequ12 rexbidv orbi12d imbi12d chvar adantlr hban anbi1d
      wbr breq2 adantllr fdc anassrs fvex dfsbcq syl5rbbr biimpcd adantl anim1d
      idd sbcie 3anim123d eximdv reximdv mpd chvarv ex rexlimdva ) FGQIUMNMUNZU
      OUPIKUNZUQZHEURZCLOXLUOUPUSZUTZKVAZMPUMZUGFGXSQIFQUNZIVBZURZGXSFUKUNZIVBZ
      URZGQYCVCZURZXSVGYBGURZXSVGUKQYCXTVHZYGYHXSYIYEYBYFGYIYDYAFYCXTIVDVEGUKQV
      FVIVJYGXNNXMVKZYCVHZEURZXPUTZKVAZMPUMZXSFYDYFYOAQULUNZVCZBCDQYPVCZEFYDYFU
      RZURIYCJKLMNOPULRSTUAUBABQULLUNVLVMUPXMVKBQVNUDVOUEDEQULXLXMVKEQVNUFVOFYD
      YFVPFIJVQYSUHVRFYPIVBZYRYQRIUMZVSZYSYBDARIUMZVSZVGFYTURZUUBVGQULUUEUUBQUU
      EQVNYRUUAQDQULVTYQQRIRUNZIVBZQVNAQULVTZWAWBWCXTYPVHZYBUUEUUDUUBUUIYAYTFXT
      YPIVDZVEUUIDYRUUCUUADQULWDUUIAYQRIAQULWDZWEWFWGUIWHWIFYQYTUUGURZUUFYPJWLZ
      YSFAURZYAUUGURZURZUUFXTJWLZVGFYQURZUULURZUUMVGQULUUSUUMQUURUULQFYQQFQVNUU
      HWJUULQVNWJUUMQVNWCUUIUUPUUSUUQUUMUUIUUNUURUUOUULUUIAYQFUUKVEUUIYAYTUUGUU
      JWKVIXTYPUUFJWMWGUJWHWNWOWPYGYNXRMPYGYMXQKYGXNXNYLXOXPXPYGXNXCYGYKHEYFYKH
      VGYEYKYFHHGQYJVCYKYFGHQYJNXMWQUCXDGQYJYCWRWSWTXAXBYGXPXCXEXFXGXHXIXJXKXH
      $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d F m n p q s x y z $.  $d A m n p q s x y z $.  $d R m n p q s x y z $.
    $( Two ways to say that a sequence respects a partial order.  (Contributed
       by Jeff Madsen, 2-Sep-2009.) $)
    seqpo $p |- ( ( R Po A /\ F : NN --> A ) ->
                  ( A. s e. NN ( F ` s ) R ( F ` ( s + 1 ) ) <-> A. m e. NN
                    A. n e. ( ZZ>= ` ( m + 1 ) ) ( F ` m ) R ( F ` n ) ) ) $=
      ( cn wa cv cfv c1 caddc wbr wral cuz wcel wi wceq fveq2 breq2d wpo imbi2d
      vp vq wf co cz oveq1 fveq2d breq12d rcla4cva adantl peano2nn elnnuz sylib
      a1i uztrn sylibr expcom syl imdistani ad2ant2l ex ffvelrn adantrr adantrl
      w3a sylan2 3jca potr exp3acom23 syl5 expdimp adantr mpdd anasss com12 a2d
      uzind4 ralrimiv anassrs ralrimiva breq1d raleqbidv rcla4v imdistanri uzid
      nnz 3syl impbid1 ) ABUAZGAEUEZHZFIZEJZWNKLUFZEJZBMZFGNZCIZEJZDIZEJZBMZDWT
      KLUFZOJZNZCGNZWMWSXHWMWSHZXGCGWMWSWTGPZXGWMWSXJHZHZXDDXFXBXFPXLXDXLXAUCIZ
      EJZBMZQXLXAXEEJZBMZQZXLXAUDIZEJZBMZQXLXAXSKLUFZEJZBMZQXLXDQUCUDXEXBXMXERZ
      XOXQXLYEXNXPXABXMXEESTUBXMXSRZXOYAXLYFXNXTXABXMXSESTUBXMYBRZXOYDXLYGXNYCX
      ABXMYBESTUBXMXBRZXOXDXLYHXNXCXABXMXBESTUBXRXEUGPXKXQWMWRXQFWTGWNWTRZWOXAW
      QXPBWNWTESYIWPXEEWNWTKLUHUIUJUKULUPXSXFPZXLYAYDXLYJYAYDQZWMWSXJYJYKQXIXJY
      JYKXJYJHXJXSGPZHZXIYKXJYJYLXJXEKOJZPZYJYLQXJXEGPYOWTUMXEUNUOYJYOYLYJYOHXS
      YNPYLXEXSKUQXSUNURUSUTVAXIYMXTYCBMZYKXIYMYPWSYLYPWMXJWRYPFXSGWNXSRZWOXTWQ
      YCBWNXSESYQWPYBEWNXSKLUHUIUJUKVBVCWMYMYPYKQZQWSWKWLYMYRWLYMHZXAAPZXTAPZYC
      APZVGZWKYRYSYTUUAUUBWLXJYTYLGAWTEVDVEWLYLUUAXJGAXSEVDVFWLYLUUBXJYLWLYBGPU
      UBXSUMGAYBEVDVHVFVIWKUUCYRWKUUCHYAYPYDAXAXTYCBVJVKVCVLVMVNVOVLVMVPVQVRVSV
      QVTWAWBVCXHWRFGXHWNGPZHWOXCBMZDWPOJZNZUUDHWRUUDXHUUGXGUUGCWNGWTWNRZXDUUED
      XFUUFUUHXEWPOWTWNKLUHUIUUHXAWOXCBWTWNESWCWDWEWFUUDUUGWPUUFPZWRUUDWPGPWPUG
      PUUIWNUMWPWHWPWGWIUUEWRDWPUUFXBWPRXCWQWOBXBWPESTUKVHUTWBWJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d F k m n p q $.  $d A k m n p q $.
    $( An increasing sequence of natural numbers takes on indefinitely large
       values.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    incsequz $p |- ( ( F : NN --> NN /\ A. m e. NN
                    ( F ` m ) < ( F ` ( m + 1 ) ) /\ A e. NN ) ->
                                  E. n e. NN ( F ` n ) e. ( ZZ>= ` A ) ) $=
      ( vk cn cv cfv c1 caddc wbr wcel cuz wrex wa wi wceq fveq2 cr syl vp wral
      vq wf co clt eleq2d rexbidv imbi2d c0 wne ne0i ax-mp ffvelrn elnnuz sylib
      1nn ralrimiva r19.2z sylancr adantr cle nnre ad2antrr adantlr adantll 1re
      wb leadd1 syl3anc oveq1 fveq2d breq12d rcla4v imdistani peano2nn nnltp1le
      a1i sylan2 syl2anc biimpa anasss anass1rs peano2re adantlrr mpan2d sylbid
      3syl letr cz eluz syl2an adantrlr anassrs peano2zdi 3imtr4d adantl jctild
      nnz eleq1d rcla4ev syl6 rexlimdva cbvrexv syl6ib a2d nnind com12 3impia
      ex ) FFDUDZBGZDHZXLIJUEZDHZUFKZBFUBZAFLZCGZDHZAMHZLZCFNZXRXKXQOZYCYDXTUAG
      ZMHZLZCFNZPYDXTIMHZLZCFNZPYDXTUCGZMHZLZCFNZPYDXTYLIJUEZMHZLZCFNZPYDYCPUAU
      CAYEIQZYHYKYDYTYGYJCFYTYFYIXTYEIMRUGUHUIYEYLQZYHYOYDUUAYGYNCFUUAYFYMXTYEY
      LMRUGUHUIYEYPQZYHYSYDUUBYGYRCFUUBYFYQXTYEYPMRUGUHUIYEAQZYHYCYDUUCYGYBCFUU
      CYFYAXTYEAMRUGUHUIXKYKXQXKFUJUKZYJCFUBYKIFLUUDUQFIULUMXKYJCFXKXSFLZOZXTFL
      ZYJFFXSDUNZXTUOUPURYJCFUSUTVAYLFLZYDYOYSUUIYDYOYSPUUIYDOZYOEGZDHZYQLZEFNZ
      YSUUJYNUUNCFUUJUUEOZYNXSIJUEZFLZUUPDHZYQLZOUUNUUOYNUUSUUQUUOYLXTVBKZYPUUR
      VBKZYNUUSUUOUUTYPXTIJUEZVBKZUVAUUOYLSLZXTSLZISLZUUTUVCVHUUIUVDYDUUEYLVCZV
      DYDUUEUVEUUIXKUUEUVEXQUUFUUGUVEUUHXTVCTVEVFUVFUUOVGVRYLXTIVIVJUUOUVCUVBUU
      RVBKZUVAYDUUEUVHUUIXKUUEXQUVHUUEXQOXKUUEXTUURUFKZOUVHUUEXQUVIXPUVIBXSFXLX
      SQZXMXTXOUURUFXLXSDRUVJXNUUPDXLXSIJVKVLVMVNVOXKUUEUVIUVHUUFUVIUVHUUFUUGUU
      RFLZUVIUVHVHUUHUUEXKUUQUVKXSVPZFFUUPDUNZVSXTUURVQVTWAWBVSWCVFUUIXKUUEUVCU
      VHOUVAPZXQUUIXKOUUEOYPSLZUVBSLZUURSLZUVNUUIUVOXKUUEUUIUVDUVOUVGYLWDTVDXKU
      UEUVPUUIUUFUUGUVBFLUVPUUHXTVPUVBVCWHVFXKUUEUVQUUIUUEXKUUQUVQUVLXKUUQOZUVK
      UVQUVMUURVCTVSVFYPUVBUURWIVJWEWFWGUUIYDUUEYNUUTVHZUUIXKUUEUVSXQUUIYLWJLXT
      WJLZUVSUUFYLWSZUUFUUGUVTUUHXTWSTYLXTWKWLWMWNUUIYDUUEUUSUVAVHZUUIXKUUEUWBX
      QUUIYPWJLUURWJLZUWBUUFUUIYLUWAWOUUEXKUUQUWCUVLUVRUVKUWCUVMUURWSTVSYPUURWK
      WLWMWNWPUUEUUQUUJUVLWQWRUUMUUSEUUPFUUKUUPQUULUURYQUUKUUPDRWTXAXBXCUUMYREC
      FUUKXSQUULXTYQUUKXSDRWTXDXEXJXFXGXHXI $.
      $( [2-Sep-2009] $)

    $( An increasing sequence of natural numbers takes on indefinitely large
       values.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    incsequz2 $p |- ( ( F : NN --> NN /\ A. m e. NN
            ( F ` m ) < ( F ` ( m + 1 ) ) /\ A e. NN ) ->
              E. n e. NN A. k e. ( ZZ>= ` n ) ( F ` k ) e. ( ZZ>= ` A ) ) $=
      ( vp vq cn cv cfv c1 caddc clt wbr wral wcel cuz wi wa cr wf w3a incsequz
      co wrex wpo wb wss nnssre wor ltso sopo ax-mp poss seqpo biimpd imdistani
      mp2 mpan wceq wo uzp1 fveq2 adantl ffvelrn nnz uzid 3syl eqeltrd adantllr
      cz adantr oveq1 fveq2d breq1d raleqbidv rcla4cva breq2d adantlll peano2nn
      sylan syl elnnuz sylib uztrn ancoms sylibr sylan2 anassrs cle ltle syl2an
      zre eluz sylibrd syl2anc mpd jaodan ex ralrimdva 3impa reximdvai ) HHEUAZ
      CIZEJXDKLUDEJMNCHOZAHPZUBZDIZEJZAQJZPZDHUEBIZEJZXJPZBXHQJZOZDHUEACDEUCXGX
      KXPDHXCXEXFXHHPZXKXPRZRZXCXESXCFIZEJZGIZEJZMNZGXTKLUDZQJZOZFHOZSZXFXSXCXE
      YHXCXEYHHMUFZXCXEYHUGHTUHTMUFZYJUITMUJYKUKTMULUMHTMUNURHMFGECUOUSUPUQYIXF
      SZXQXRYLXQSXKXNBXOYIXQXLXOPZXKXNRZXFYIXQSZYMSXMXIQJZPZYNYMYOXLXHUTZXLXHKL
      UDZQJZPZVAYQXHXLVBYOYRYQUUAXCXQYRYQYHXCXQSZYRSXMXIYPYRXMXIUTUUBXLXHEVCVDU
      UBXIYPPZYRUUBXIHPZXIVKPZUUCHHXHEVEZXIVFZXIVGVHVLVIVJYOUUASXIXMMNZYQYHXQUU
      AUUHXCYHXQSXIYCMNZGYTOZUUAUUHYGUUJFXHHXTXHUTZYDUUIGYFYTUUKYEYSQXTXHKLVMVN
      UUKYAXIYCMXTXHEVCVOVPVQUUIUUHGXLYTYBXLUTYCXMXIMYBXLEVCVRVQWAVSXCXQUUAUUHY
      QRZYHUUBUUASUUEXMVKPZUULUUBUUEUUAUUBUUDUUEUUFUUGWBVLXCXQUUAUUMXQUUASXCXLH
      PZUUMXQYSKQJZPZUUAUUNXQYSHPUUPXHVTYSWCWDUUPUUASXLUUOPZUUNUUAUUPUUQYSXLKWE
      WFXLWCWGWAXCUUNSXMHPUUMHHXLEVEXMVFWBWHWIUUEUUMSUUHXIXMWJNZYQUUEXITPXMTPUU
      HUURRUUMXIWMXMWMXIXMWKWLXIXMWNWOWPVJWQWRWHYQXKXNXIXMAWEWSWBVJWTWSWAXAXBWQ
      $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( A bounded above set of natural numbers is finite.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    nnubfi $p |- ( ( A C_ NN /\ B e. NN ) -> { x e. A | x < B } e. Fin ) $=
      ( cn wss wcel wa cc0 cfz cfn wbr wi cn0 adantl ad2antrr nnnn0 syl adantlr
      cr nnre co cv clt crab fzfi wral cle w3a wb elfz2nn0 adantr ad2antlr ltle
      ssel2 syl2anc imp mpbir3and ex ralrimiva rabss sylibr ssfi sylancr ) BDEZ
      CDFZGZHCIUAZJFAUBZCUCKZABUDZVGEZVJJFHCUEVFVIVHVGFZLZABUFVKVFVMABVFVHBFZGZ
      VIVLVOVIGVLVHMFZCMFZVHCUGKZVFVLVPVQVRUHUIZVNVIVEVSVDDVHCUJNOVOVPVIVDVNVPV
      EVDVNGZVHDFZVPBDVHUNZVHPQRUKVFVQVNVIVEVQVDCPNOVOVIVRVOVHSFZCSFZVIVRLVDVNW
      CVEVTWAWCWBVHTQRVEWDVDVNCTULVHCUMUOUPUQURUSVIABVGUTVAVGVJVBVC $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( An infinite set of natural numbers is unbounded above.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    nninfnub $p |- ( ( A C_ NN /\ -. A e. Fin /\ B e. NN )
                                  -> { x e. A | B < x } =/= (/) ) $=
      ( vy cn wss cfn wcel wn cv clt wbr c0 wa wal wral wi sylibr cr nnre breq2
      crab wne wceq eq0 elrab notbii imnan bitr4i biimpi alimi df-ral cle ssel2
      syl adantlr ad2antlr lenlt biimprd syl2anc ralimdva cc0 cfz co fzfi nnnn0
      cn0 w3a adantr adantl ad2antrr simpr 3jca ex wb elfz2nn0 sylibrd imp ssfi
      dfss3 sylancr syld syl5 syl5bi necon3bd an32s 3impa ) BEFZBGHZIZCEHZCAJZK
      LZABUBZMUCZWHWKWJWOWHWKNZWJWOWPWIWNMWNMUDDJZWNHZIZDOZWPWIDWNUEWTCWQKLZIZD
      BPZWPWIWTWQBHZXBQZDOXCWSXEDWSXEWSXDXANZIXEWRXFWMXAAWQBWLWQCKUAUFUGXDXAUHU
      IUJUKXBDBULRWPXCWQCUMLZDBPZWIWPXBXGDBWPXDNZWQSHZCSHZXBXGQWHXDXJWKWHXDNZWQ
      EHZXJBEWQUNZWQTUOUPWKXKWHXDCTUQXJXKNXGXBWQCURUSUTVAWPXHWIWPXHNZVBCVCVDZGH
      BXPFZWIVBCVEXOWQXPHZDBPZXQWPXHXSWPXGXRDBXIXGWQVGHZCVGHZXGVHZXRXIXGYBXIXGN
      XTYAXGXIXTXGWHXDXTWKXLXMXTXNWQVFUOUPVIWPYAXDXGWKYAWHCVFVJVKXIXGVLVMVNWKXR
      YBVOWHXDEWQCVPUQVQVAVRDBXPVTRXPBVSWAVNWBWCWDWEVRWFWG $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d k x A $.  $d x B $.  $d x C $.  $d k x ph $.
    csbrn.1 $e |- ( ph -> A e. Fin ) $.
    csbrn.2 $e |- ( ( ph /\ k e. A ) -> B e. RR ) $.
    csbrn.3 $e |- ( ( ph /\ k e. A ) -> C e. RR ) $.
    $( Cauchy-Schwarz-Bunjakovsky inequality for R^n.  (Contributed by Jeff
       Madsen, 2-Sep-2009.)  (Revised by Mario Carneiro, 4-Jun-2014.) $)
    csbrn $p |- ( ph -> ( sum_ k e. A ( B x. C ) ^ 2 ) <_
                        ( sum_ k e. A ( B ^ 2 ) x. sum_ k e. A ( C ^ 2 ) ) ) $=
      ( cmul co csu c2 cexp cle cc wcel cr syl2anc recnd caddc vx wbr c4 2cn cv
      wceq remulcl fsumrecl sqmul sylancr sq2 oveq1i syl6eq cmin cc0 resqcl syl
      wa 2re cfn adantr adantlr simplr readdcl sqge0 binom2 mul32 oveq2d mulass
      syl3anc a1i eqtr4d oveq12d eqtrd breqtrd fsumge0 addcl fsumadd simpr sqcl
      oveq1d fsummulc1 fsummulc2 discr wb 4re suble0 mpbid eqbrtrrd 4pos lemul2
      clt syl112anc mpbird ) ABCDIJZEKZLMJZBCLMJZEKZBDLMJZEKZIJZNUBZUCWQIJZUCXB
      IJZNUBZALWPIJZLMJZXDXENAXHLLMJZWQIJZXDALOPZWPOPXHXJUFUDAWPABWOEFAEUEBPZUR
      ZCQPZDQPZWOQPZGHCDUGRZUHZSLWPUIUJXIUCWQIUKULUMAXHXEUNJUONUBZXHXENUBZAUAWS
      XGXAABWREFXMXNWRQPZGCUPUQZUHZALQPZWPQPZXGQPZUSXRLWPUGUJZABWTEFXMXOWTQPZHD
      UPUQZUHZAUAUEZQPZURZUOBWRYKLMJZIJZLWOIJZYKIJZTJZWTTJZEKZWSYNIJZXGYKIJZTJZ
      XATJZNYMBYSEABUTPYLFVAZYMXLURZYRQPZYHYSQPUUFYOQPZYQQPZUUGUUFYAYNQPZUUHAXL
      YAYLYBVBZUUFYLUUJAYLXLVCZYKUPUQWRYNUGRZUUFYPQPZYLUUIAXLUUNYLXMYDXPUUNUSXQ
      LWOUGUJZVBUULYPYKUGRZYOYQVDRAXLYHYLYIVBZYRWTVDRUUFUOCYKIJZDTJZLMJZYSNUUFU
      USQPZUOUUTNUBUUFUURQPZXOUVAUUFXNYLUVBAXLXNYLGVBZUULCYKUGRZAXLXOYLHVBZUURD
      VDRUUSVEUQUUFUUTUURLMJZLUURDIJZIJZTJZWTTJZYSUUFUUROPDOPZUUTUVJUFUUFUURUVD
      SUUFDUVESZUURDVFRUUFUVIYRWTTUUFUVFYOUVHYQTUUFCOPZYKOPZUVFYOUFUUFCUVCSZUUF
      YKUULSZCYKUIRUUFUVHLWOYKIJZIJZYQUUFUVGUVQLIUUFUVMUVNUVKUVGUVQUFUVOUVPUVLC
      YKDVGVJVHUUFXKWOOPUVNYQUVRUFXKUUFUDVKUUFWOAXLXPYLXQVBSZUVPLWOYKVIVJVLVMWA
      VNVOVPYMYTBYREKZXATJUUDYMBYRWTEUUEUUFYOOPYQOPYROPUUFYOUUMSZUUFYQUUPSZYOYQ
      VQRUUFWTUUQSVRYMUVTUUCXATYMUVTBYOEKZBYQEKZTJUUCYMBYOYQEUUEUWAUWBVRYMUUAUW
      CUUBUWDTYMBWRYNEUUEYMUVNYNOPYMYKAYLVSSZYKVTUQUUFWRUUKSWBYMUUBBYPEKZYKIJUW
      DYMXGUWFYKIYMBWOLEUUEXKYMUDVKUVSWCWAYMBYPYKEUUEUWEAXLYPOPYLXMYPUUOSVBWBVN
      VMVLWAVNVOWDAXHQPZXEQPZXSXTWEAYFUWGYGXGUPUQAUCQPZXBQPZUWHWFAWSQPXAQPUWJYC
      YJWSXAUGRZUCXBUGUJXHXEWGRWHWIAWQQPZUWJUWIUOUCWLUBZXCXFWEAYEUWLXRWPUPUQUWK
      UWIAWFVKUWMAWJVKWQXBUCWKWMWN $.
      $( [4-Jun-2014] $) $( [2-Sep-2009] $)

    $( Triangle inequality in R^n.  (Contributed by Jeff Madsen, 2-Sep-2009.)
       (Revised by Mario Carneiro, 4-Jun-2014.) $)
    trirn $p |- ( ph -> ( sqr ` sum_ k e. A ( ( B + C ) ^ 2 ) ) <_
     ( ( sqr ` sum_ k e. A ( B ^ 2 ) ) + ( sqr ` sum_ k e. A ( C ^ 2 ) ) ) ) $=
      ( caddc co c2 cexp cle wbr wcel cr syl recnd syl2anc cc0 csu csqr cmul cv
      cfv wa resqcl 2re remulcl sylancr fsumadd 2cn a1i fsummulc2 cabs fsumrecl
      cc abscl sqge0 fsumge0 mulge0 syl22anc resqrcl leabs wceq absresq resqrth
      csbrn 3brtr4d absge0 sqrge0 le2sq mpbird letrd clt lemul2 syl112anc mpbid
      2pos eqbrtrrd leadd2 syl3anc eqbrtrd leadd1 binom2 sumeq2dv 3eqtrd sqrmul
      wb readdcl eqcomd oveq2d oveq12d eqtrd addge0 ) ABCDIJZKLJZEUAZUBUEZBCKLJ
      ZEUAZUBUEZBDKLJZEUAZUBUEZIJZMNZWSKLJZXFKLJZMNZABWTKCDUCJZUCJZIJZEUAZXDIJZ
      XAKXAXDUCJZUBUEZUCJZIJZXDIJZXHXIMAXNXSMNZXOXTMNZAXNXABXLEUAZIJZXSMABWTXLE
      FAEUDBOUFZWTYECPOZWTPOZGCUGQZRYEXLYEKPOZXKPOZXLPOZUHYEYFDPOZYJGHCDUISZKXK
      UIUJZRUKAYCXRMNZYDXSMNZAKBXKEUAZUCJZYCXRMABXKKEFKUQOAULUMYEXKYMRUNAYQXQMN
      ZYRXRMNZAYQYQUOUEZXQABXKEFYMUPZAYQUQOZUUAPOZAYQUUBRZYQURQZAXPPOZTXPMNZXQP
      OZAXAPOZXDPOZUUGABWTEFYHUPZABXCEFYEYLXCPOHDUGQZUPZXAXDUISZAUUJTXAMNZUUKTX
      DMNZUUHUULABWTEFYHYEYFTWTMNGCUSQUTZUUNABXCEFUUMYEYLTXCMNHDUSQUTZXAXDVAVBZ
      XPVCSZAYQPOZYQUUAMNUUBYQVDQAUUAXQMNZUUAKLJZXQKLJZMNZAYQKLJZXPUVDUVEMABCDE
      FGHVHAUVBUVDUVGVEUUBYQVFQAUUGUUHUVEXPVEUUOUUTXPVGSVIAUUDTUUAMNZUUITXQMNZU
      VCUVFWIUUFAUUCUVHUUEYQVJQUVAAUUGUUHUVIUUOUUTXPVKSUUAXQVLVBVMVNAUVBUUIYITK
      VONZYSYTWIUUBUVAYIAUHUMUVJAVSUMYQXQKVPVQVRVTAYCPOXRPOZUUJYOYPWIABXLEFYNUP
      AYIUUIUVKUHUVAKXQUIUJZUULYCXRXAWAWBVRWCAXNPOXSPOZUUKYAYBWIABXMEFYEYGYKXMP
      OYHYNWTXLWJSZUPAUUJUVKUVMUULUVLXAXRWJSUUNXNXSXDWDWBVRAXHWRBXMXCIJZEUAXOAW
      RPOZTWRMNZXHWRVEABWQEFYEWPPOZWQPOYEYFYLUVRGHCDWJSZWPUGQZUPZABWQEFUVTYEUVR
      TWQMNUVSWPUSQUTZWRVGSABWQUVOEYECUQODUQOWQUVOVEYECGRYEDHRCDWESWFABXMXCEFYE
      XMUVNRYEXCUUMRUKWGAXIXBKLJZKXBXEUCJZUCJZIJZXEKLJZIJZXTAXBUQOXEUQOXIUWHVEA
      XBAUUJUUPXBPOZUULUURXAVCSZRAXEAUUKUUQXEPOZUUNUUSXDVCSZRXBXEWESAUWFXSUWGXD
      IAUWCXAUWEXRIAUUJUUPUWCXAVEUULUURXAVGSAUWDXQKUCAXQUWDAUUJUUPUUKUUQXQUWDVE
      UULUURUUNUUSXAXDWHVBWKWLWMAUUKUUQUWGXDVEUUNUUSXDVGSWMWNVIAWSPOZTWSMNZXFPO
      ZTXFMNZXGXJWIAUVPUVQUWMUWAUWBWRVCSAUVPUVQUWNUWAUWBWRVKSAUWIUWKUWOUWJUWLXB
      XEWJSAUWIUWKTXBMNZTXEMNZUWPUWJUWLAUUJUUPUWQUULUURXAVKSAUUKUUQUWRUUNUUSXDV
      KSXBXEWOVBWSXFVLVBVM $.
      $( [4-Jun-2014] $) $( [2-Sep-2009] $)
  $}

