$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Boundedness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TotBnd $.
  $c Bnd $.
  $( Extend class notation with the class of totally bounded metric spaces. $)
  ctotbnd $a class TotBnd $.
  $( Extend class notation with the class of bounded metric spaces. $)
  cbnd $a class Bnd $.

  ${
    $d m d v b x $.
    $( Define the class of totally bounded metrics.  A metric space is totally
       bounded iff it can be covered by a finite number of balls of any given
       radius. $)
    df-totbnd $a |- TotBnd = { m e. Met | A. d e. RR+ E. v e. Fin ( U. v =
      dom dom m /\ A. b e. v E. x e. dom dom m b = ( x ( ball ` m ) d ) ) } $.
  $}

  ${
    $d M m d v b x $.  $d X m d v b x $.
    istotbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space".  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd $p |- ( M e. TotBnd <-> ( M e. Met /\ A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vm cv cdm wceq cbl cfv co wrex wral wa cfn crp eqeq2d cuni ctotbnd dmeq
      cme dmeqd syl6eqr fveq2 oveqd rexeqbidv ralbidv anbi12d rexbidv df-totbnd
      elrab2 ) BIZUAZHIZJZJZKZEIZAIZFIZUQLMZNZKZAUSOZEUOPZQZBROZFSPUPDKZVAVBVCC
      LMZNZKZADOZEUOPZQZBROZFSPHCUDUBUQCKZVJVRFSVSVIVQBRVSUTVKVHVPVSUSDUPVSUSCJ
      ZJDVSURVTUQCUCUEGUFZTVSVGVOEUOVSVFVNAUSDWAVSVEVMVAVSVDVLVBVCUQCLUGUHTUIUJ
      UKULUJABHEFUMUN $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.  $d X d v b x $.
    istotbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a totally bounded metric space."  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    istotbnd2 $p |- ( M e. Met -> ( M e. TotBnd <-> A. d e. RR+ E. v e. Fin
        ( U. v = X /\ A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( ctotbnd wcel cme cv cuni wceq cbl cfv co wrex wral wa cfn istotbnd baib
      crp ) CHICJIBKZLDMEKAKFKCNOPMADQEUDRSBTQFUCRABCDEFGUAUB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M d v b x $.
    $( The predicate "totally bounded" implies ` M ` is a metric space.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndmet $p |- ( M e. TotBnd -> M e. Met ) $=
      ( vv vb vx vd ctotbnd wcel cme cv cuni cdm wceq cbl cfv wrex wral cfn crp
      co wa eqid istotbnd simplbi ) AFGAHGBIZJAKKZLCIDIEIAMNSLDUEOCUDPTBQOERPDB
      AUECEUEUAUBUC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d X a b c d r s t u v w x y z $.  $d M a b c d r s t u v w x y z $.
    $d Y a b c d r s t u v w x y z $.  $d N a b c d r s t u v w x y z $.
    sstotbnd.1 $e |- X = dom dom M $.
    sstotbnd.2 $e |- N = ( M |` ( Y X. Y ) ) $.
    $( Condition for a subset of a metric space to be totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    sstotbnd $p |- ( ( M e. Met /\ Y C_ X ) -> ( N e. TotBnd <->
                        A. d e. RR+ E. v e. Fin ( Y C_ U. v /\
                          A. b e. v E. x e. X b = ( x ( ball ` M ) d ) ) ) ) $=
      ( vs vt vy wcel wss wa wceq wrex wral cfn vw vu vz va vc cme ctotbnd cuni
      vr cv cbl cfv co crp cdm wb cxp cres metres adantr syl5eqel istotbnd2 syl
      eqid w3a cvv simplr resexg dmexg 3syl ad2antrr simprr indexfi syl3anc cab
      wi abrexfi adantl ad2antlr metssba2 syl6reqr eqeq2d biimpa eqcomd adantlr
      dmeqi vex eqeq1 rexbidv elab biimpri ralimi dfss3 sylibr ad2antll rcla4ev
      oveq1 mpan2 ovex cin simpr sseq2d adantllr blssp anass1rs eqsstrd syl2anc
      a1i ex rexlimdva syl5bi ralrimiv adantrr adantll ssrexv cbvrexv 3ad2antr1
      adantlrr unieq raleq anbi12d anassrs mpd c0 wne ineq1 neeq1d elrab sylan2
      jcad eluni2 wex elin syl6 ssel2 imp rcla4cva ad2ant2rl eleq2 syld c2 cdiv
      uniss sselda syldan eqtrd inss1 sseq2 uniss2 sstrd biimpd imdistani sstr2
      3adantr3 impcom syl6ib syl12anc ralimdva sylbid oveq2 ralbidv anbi2d crab
      rphalfcl rcla4v inelcm expcom 3imtr4g ralimia 3imtr4i ssrab2 ssralv ax-mp
      reximdv2 ancom bitri exbii n0 df-rex 3bitr4i biimpi rgen 3jca ssfi jctild
      sylbi 3anbi123d ssexg ancoms 3expb an12s blssm 3com23 3expia sylan eleq2d
      imbi12d mpbid sseq1 syl5ibrcom unissb sylib elequ1 elequ2 syl5bb adantlll
      a1d 3ad2antl2 ad2ant2lr biimpcd reximdv blhalf an4s sseld expimpd ancomsd
      sylanr1 adantlrl simpll sylanbrc 3ad2antl1 eleqtrrd ssrdv eqssd ad3antrrr
      id adantrl eqtr4d biimpar jctird eqeq1d expdimp com23 exp3a 3impd sylibrd
      ralrimdva impbid ) CUFNZFEOZPZDUGNZFBUJZUHZOZGUJZAUJZHUJZCUKULZUMZQZAERZG
      VUCSZPZBTRZHUNSZVUAVUBUAUJZUHZDUOZUOZQZUBUJZKUJZVUHDUKULZUMZQZKVUTRUBVUQS
      ZPZUATRZHUNSZVUPVUADUFNZVUBVVJUPVUADCFFUQZURZUFJUYSVVMUFNUYTCFUSUTVAZKUAD
      VUTUBHVUTVDZVBVCVUAVVIVUOHUNVUAVUHUNNZPZVVHVUOUATVVQVUQTNZPZVVHVUOVVSVVHP
      ZUCUJZVUTOZVVFKVWARZUBVUQSZVVFUBVUQRKVWASZVEZUCTRZVUOVVTVVRVUTVFNZVVGVWGV
      VQVVRVVHVGVVQVWHVVRVVHUYSVWHUYTVVPUYSDVFNVUSVFNVWHUYSDVVMVFJCVVLUFVHVADVF
      VIVUSVFVIVJVKVKVVSVVAVVGVLVVFUBKVUQVUTVFUCVMVNVVSVVAVWGVUOVPVVGVVSVVAPVWF
      VUOUCTVVSVVAVWATNZVWFVUOVPVVSVVAVWIPZPZVWFVUOVWKVWFPUDUJZLUJZVUHVUIUMZQZL
      VWARZUDVOZTNZFVWQUHZOZVULGVWQSZVUOVWJVWRVVSVWFVWIVWRVVALUDVWAVWNVQVRVSVWK
      VWBVWDVWTVWEVVSVVAVWBVWDPZVWTVWIVVQVVAVXBVWTVVRVVQVVAPZVXBPFVURVWSVXCFVUR
      QZVXBVUAVVAVXDVVPVUAVVAPVURFVUAVVAVURFQVUAVUTFVURVUAFVVMUOZUOVUTCEFIVTVUS
      VXEDVVMJWFWFWAZWBWCWDWEUTVVQVXBVURVWSOVVAVVQVXBPZVURVUFVVEQZKVWARZGVOZUHZ
      VWSVXGVUQVXJOZVURVXKOVWDVXLVVQVWBVWDVVBVXJNZUBVUQSVXLVWCVXMUBVUQVXMVWCVXI
      VWCGVVBUBWGVUFVVBQVXHVVFKVWAVUFVVBVVEWHWIWJWKWLUBVUQVXJWMWNWOVUQVXJUUCVCV
      XGVUGMUJZOZMVWQRZAVXJSZVXKVWSOVVQVWBVXQVWDVVQVWBPZVXPAVXJVUGVXJNVUGVVEQZK
      VWARZVXRVXPVXIVXTGVUGAWGZVUFVUGQVXHVXSKVWAVUFVUGVVEWHWIWJVXRVXSVXPKVWAVXR
      VVCVWANZPZVXSVXPVYCVXSPZVVCVUHVUIUMZVWQNZVUGVYEOZVXPVYBVYFVXRVXSVYBVYEVWN
      QZLVWARZVYFVYBVYEVYEQZVYIVYEVDVYHVYJLVVCVWAVWMVVCQZVWNVYEVYEVWMVVCVUHVUIW
      QWBWPWRVWPVYIUDVYEVVCVUHVUIWSVWLVYEQVWOVYHLVWAVWLVYEVWNWHWIWJWNVSVYDVUGVY
      EFWTZVYEVYDVUGVVEVYLVYCVXSXAVYCVVEVYLQZVXSVXRVYBVVCFNZVYMVUAVWBVYBVYNVVPV
      UAVWBPZVWAFVVCVUAVWBVWAFOZVUAVUTFVWAVXFXBZWCUUDXCVVQVYNVYMVWBVUAVYNVVPVYM
      VUHFCDEVVCIJXDXEWEUUEUTUUFVYLVYEOVYDVYEFUUGXHXFVXOVYGMVYEVWQVXNVYEVUGUUHW
      PXGXIXJXKXLXMAMVXJVWQUUIVCUUJWEXFXCXRUUNVWKVWDVWBVXAVWEVVSVWBVXAVWJVVQVWB
      VXAVVRVUAVWBVXAVVPVYOVUAVYPPVWAEOZVXAVUAVWBVYPVUAVWBVYPVYQUUKUULUYTVYPVYR
      UYSVYPUYTVYRVWAFEUUMUUOXNVYRVULGVWQVUFVWQNVUFVWNQZLVWARZVYRVULVWPVYTUDVUF
      GWGVWLVUFQVWOVYSLVWAVWLVUFVWNWHWIWJVYRVYTVYSLERVULVYSLVWAEXOVYSVUKLAEVWMV
      UGQVWNVUJVUFVWMVUGVUHVUIWQWBXPUUPXKXLVJWEWEWEXQVUNVWTVXAPBVWQTVUCVWQQZVUE
      VWTVUMVXAWUAVUDVWSFVUCVWQXSXBVULGVUCVWQXTYAWPUUQXIYBXJXMYCXIXJUURUUSVUAVU
      PVVAVVCVUHUEUJZVVDUMZQZHVUTRZKVUQSZPZUATRZUEUNSZVUBVUAVUPWUHUEUNVUAWUBUNN
      ZPZVUPVUEVUFVUGWUBUUAUUBUMZVUIUMZQZAERZGVUCSZPZBTRZWUHWUJVUPWURVPZVUAWUJW
      ULUNNWUSWUBUVDVUOWURHWULUNVUHWULQZVUNWUQBTWUTVUMWUPVUEWUTVULWUOGVUCWUTVUK
      WUNAEWUTVUJWUMVUFVUHWULVUGVUIUUTWBWIUVAUVBWIUVEVCVRWUKWUQWUHBTWUKVUCTNZPZ
      WUQFVVBUHZOZWUOGVVBSZVXNVWMNZMFRZLVVBSZVEZUBTRZWUHWVBWUQVWLFWTZYDYEZUDVUC
      UVCZTNZFWVMUHZOZWUOGWVMSZWVGLWVMSZVEZPWVJWVBWUQWVSWVNWUQWVSVPWVBWUQWVPWVQ
      WVRVUEWVPWUPVXNVUDNZMFSVXNWVONZMFSVUEWVPWVTWWAMFVXNFNZVXNVUGNZAVUCRWWCAWV
      MRWVTWWAWWBWWCWWCAVUCWVMWWBVUGVUCNZWWCPZVUGWVMNZWWCWWEWWBWWFWWDWWCWWBWWFW
      WCWWBPWWDVUGFWTZYDYEZWWFVXNVUGFUVFWWFWWDWWHPWVLWWHUDVUGVUCVWLVUGQZWVKWWGY
      DVWLVUGFYFYGYHWKYIYBUVGWWEWWCVPWWBWWDWWCXAXHYJUVNAVXNVUCYKAVXNWVMYKUVHUVI
      MFVUDWMMFWVOWMUVJUTWUPWVQVUEWVMVUCOZWUPWVQVPWVLUDVUCUVKZWUOGWVMVUCUVLUVMV
      RWVRWUQWVGLWVMVWMWVMNVWMVUCNZVWMFWTZYDYEZPWVGWVLWWNUDVWMVUCVWLVWMQWVKWWMY
      DVWLVWMFYFYGYHWWNWVGWWLWWNWVGVXNWWMNZMYLWWBWVFPZMYLWWNWVGWWOWWPMWWOWVFWWB
      PWWPVXNVWMFYMWVFWWBUVOUVPUVQMWWMUVRWVFMFUVSUVTUWAVRUWFUWBXHUWCXHWVAWVNWUK
      WVAWWJWVNWWKVUCWVMUWDWRVRUWEWVIWVSUBWVMTVVBWVMQZWVDWVPWVEWVQWVHWVRWWQWVCW
      VOFVVBWVMXSXBWUOGVVBWVMXTWVGLVVBWVMXTUWGWPYNWUKWVJWUHVPWVAWUKWVIWUHUBTWUK
      VVBTNZPZWVDWVEWVHWUHWWSWVDWVEWVHWUHVPWWSWVHWVDWVEPZWUHWUKWWRWVHWWTWUHVPZW
      UKWWRWVHPZVUCFOZWVFMVUCRZLVVBSZWVFLVVBRMVUCSZVEZBTRZWXAVUAWXBWXHVPZWUJVUA
      FVFNZWXIUYTUYSWXJUYSUYTEVFNWXJUYSECUOZUOZVFIUYSWXKVFNWXLVFNCUFVIWXKVFVIVC
      VAFEVFUWHYIUWIWXJWXBWXHWWRWXJWVHWXHWWRWXJWVHWXHWVFLMVVBFVFBVMUWJUWKXIVCUT
      WUKWXGWXABTVUAWUJWVAWXGWXAVPVUAWUJWVAPZPZWXGWXAWXNWXGPZWWTVWLVWAWUBVVDUMZ
      QZUCVUCRZUDVOZTNZWXSUHZVUTQZWUEKWXSSZPZPWUHWXOWWTWXTWYDWXMWWTWXTVPZVUAWXG
      WVAWYEWUJWVAWXTWWTUCUDVUCWXPVQUXGVRVSWXOWWTWYBWYCWXOWWTWYBWXOWWTPZWYAFVUT
      WYFWYAFWXOWYAFOZWWTWXNWXEWXCWYGWXFVUAWUJWXCWYGWVAWUKWXCPZVUGFOZAWXSSWYGWY
      HWYIAWXSVUGWXSNVUGWXPQZUCVUCRZWYHWYIWXRWYKUDVUGVYAWWIWXQWYJUCVUCVWLVUGWXP
      WHWIWJWYHWYJWYIUCVUCWYHVWAVUCNZPWYIWYJWXPFOZWUKWXCWYLWYMWXCWYLPWUKVWAFNZW
      YMVUCFVWAYOWUKWYNWYMWUKVWAVUTNZWXPVUTOZVPZWYNWYMVPZVUAVVKWUJWYQVVNVVKWUJW
      YOWYPVVKWYOWUJWYPDVWAWUBVUTVVOUWLUWMUWNUWOVUAWYQWYRUPWUJVUAWYOWYNWYPWYMVU
      AVUTFVWAVXFUWPVUAVUTFWXPVXFXBUWQUTUWRYPYIYBVUGWXPFUWSUWTXJXKXLAWXSFUXAWNX
      RXQUTWYFUAFWYAWYFVUQFNZVUQWYANZWYFWYSPZVUQVUHNZHWXSRZWYTXUAVUQVVCNZKVVBRZ
      XUCWWTWYSXUEWXOWVDWYSXUEWVEWVDWYSPVUQWVCNXUEFWVCVUQYOKVUQVVBYKUXBWEXNXUAX
      UDXUCKVVBWYFWYSVVCVVBNZXUDXUCVPWYFWYSXUFPZPZXUDXUCXUHXUDPZUIUJZVVCNZUIVUC
      RZXUCXUHXULXUDWXGWWTXUGXULWXNWXGXUFXULWWTWYSWXEWXCXUFXULWXFWXDXULLVVCVVBW
      XDXUJVWMNZUIVUCRVYKXULWVFXUMMUIVUCMUILUXCXPVYKXUMXUKUIVUCLKUIUXDWIUXEYQUX
      HYRUXFUTXUIXUKXUCUIVUCXUHXUDXUJVUCNZXUKXUCVPZWYFXUGXUDXUNPZXUOWYFXUGXUPPZ
      PZXUKXUCXURXUKPZXUJWUBVVDUMZWXSNZVUQXUTNZXUCXUQXUKXVAWYFXUPXVAXUGXUKXUNXV
      AXUDXUNXUTWXPQZUCVUCRZXVAXUNXUTXUTQZXVDXUTVDXVCXVEUCXUJVUCVWAXUJQWXPXUTXU
      TVWAXUJWUBVVDWQWBWPWRWXRXVDUDXUTXUJWUBVVDWSVWLXUTQWXQXVCUCVUCVWLXUTWXPWHW
      IWJWNVRVSXNXUSVUQXUJWUBVUIUMZFWTZXUTXUSVUQXVFNZWYSVUQXVGNWYFXUQXUKXVHWXNW
      WTXUQXUKPZXVHWXGWXNWVEXVIXVHWVDXUQWXNWVEPXUFXUDPZXUKXVHXUFXUDXVJWYSXUNXVJ
      UYFUXIWXNWVEXVJXUKPZXVHWVEXVKPZWXNVUQWUMNZXUJWUMNZPZAERZXVHXVLVVCWUMQZAER
      ZXVPWVEXVJXVRXUKWVEXUFXVRXUDWUOXVRGVVCVVBVUFVVCQWUNXVQAEVUFVVCWUMWHWIYQXM
      XMXVKXVRXVPVPZWVEXUDXUKXVSXUFXUDXUKPZXVQXVOAEXVQXVTXVOXVQXUDXVMXUKXVNVVCW
      UMVUQYSVVCWUMXUJYSYAUXJUXKXNVRYCVUAWUJXVPXVHWVAWUKXVPXVHWUKXVOXVHAEWUKVUG
      ENZPXVNXVMXVHUYSWUJXWAXVNXVMPXVHVPUYTUYSWUJPZXWAPXVNXVMXVHXWBXWAXVNXVMXVH
      VPXWBXWAXVNPPWUMXVFVUQUYSXWAWUJXVNWUMXVFOWUBCEVUGXUJIUXLUXMUXNYBUXOXCUXPX
      JYPXRYIYBUXQUXRXCYBXUQWYSWYFXUKWYSXUFXUPUXSVSVUQXVFFYMUXTXURXUTXVGQZXUKWY
      FXUPXWCXUGWXOXUNXWCWWTXUDWXNWXGXUNXWCWXGXUNPWXNXUJFNZXWCWXCWXEXUNXWDWXFVU
      CFXUJYOUYAVUAWUJXWDXWCWVAVUAXWDWUJXWCWUBFCDEXUJIJXDXEXRYIYBYRUYGUTUYBXUBX
      VBHXUTWXSVUHXUTVUQYSWPXGXIYBYBXJYCXIYBXJYCHVUQWXSYKWNXIUYCUYDVUAVUTFQWXMW
      XGWWTVXFUYEUYHXIWXNWXEWXCWYCWXFVUAWXCWYCWXMVUAWXCPZWUEKWXSVVCWXSNVVCWXPQZ
      UCVUCRZXWEWUEWXRXWGUDVVCKWGVWLVVCQWXQXWFUCVUCVWLVVCWXPWHWIWJXWGWUDHVUCRZX
      WEWUEXWFWUDUCHVUCVWAVUHQWXPWUCVVCVWAVUHWUBVVDWQWBXPXWEVUCVUTOZXWHWUEVPVUA
      XWIWXCVUAVUTFVUCVXFXBUYIWUDHVUCVUTXOVCXKXKXLWEXQUYJYJWUGWYDUAWXSTVUQWXSQZ
      VVAWYBWUFWYCXWJVURWYAVUTVUQWXSXSUYKWUEKVUQWXSXTYAWPYNXIYBXJYTUYLUYMUYNUYO
      XJUTYTXJYTUYQVUAVVKVUBWUIUPVVNHUADVUTKUEVVOVBVCUYPUYR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M n p q r s t u v w x y z $.  $d X n p q r s t u v w x y z $.
    $d S n p q r s t u v w x y z $.
    totbndss.1 $e |- X = dom dom M $.
    $( A subset of a totally bounded metric space is totally bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    totbndss $p |- ( ( M e. Met /\ M e. TotBnd /\ S C_ X ) ->
                                      ( M |` ( S X. S ) ) e. TotBnd ) $=
      ( vx vw vy vu vt wcel wss wi wa cv wceq wrex wral cfn cvv ex vv vz vq cme
      vr vp vn vs ctotbnd cxp cres cuni cbl cfv co crp cdm cdiv rphalfcl eqeq2d
      c2 oveq2 rexbidv ralbidv anbi2d rcla4v syl adantl cpw cin c0 wne crab w3a
      ssrab2 ssfi mpan2 ad2antlr dmexg ssexg mpan9 pwexg ad3antrrr ineq1 neeq1d
      syl5eqel elrab wex n0 eqeq1 rcla4cva adantll inss1 sseli syl5ibcom blhalf
      eleq2 inss2 ovex inex1 elpw mpbir sseq1 biimparc ssrin eqidd oveq1 ineq1d
      3ad2antl2 rcla4ev syl2anc 3ad2ant3 adantr sseq2 anbi12d syl12anc ad2antrr
      a1i 3exp1 an4s impancom anassrs imp mpdd rexlimdva exlimdv sylan2 sylan2b
      com23 adantlr biimprd ancoms syl6ibr syld jcad eqid reximdva wb istotbnd2
      sylibd mpd exp3a anasss ralrimiva indexfi syl3anc uniss syl6sseq 3ad2ant1
      unipw ssel eluni syl6ib ad2antrl inelcm biimpri an13s sseq1d rcla4cv syl5
      anbi1d expdimp ssel2 sylan2br a1d expimpd reximdv eluni2 adantrr adantllr
      elin ssrdv 3adantr3 eqss metssba2 blssp anass1rs rexeqdv adantld ralimdva
      rexlimdvw impr 3adantr2 ralrimdva metres 3imtr4d 3imp ) BUDJZBUIJZACKZBAA
      UJUKZUIJZUWHUWJUWIUWLUWHUWJUWIUWLLUWHUWJMZUANZULZCOZUBNZENZUCNZBUMUNZUOZO
      ZECPZUBUWNQZMZUARPZUCUPQZFNZULZUWKUQUQZOZGNZHNZUENZUWKUMUNUOZOZHUXJPZGUXH
      QZMZFRPZUEUPQZUWIUWLUWMUXGUXTUEUPUWMUXNUPJZMZUXGUWPUWQUWRUXNVAURUOZUWTUOZ
      OZECPZUBUWNQZMZUARPZUXTUYBUXGUYJLZUWMUYBUYDUPJUYKUXNUSUXFUYJUCUYDUPUWSUYD
      OZUXEUYIUARUYLUXDUYHUWPUYLUXCUYGUBUWNUYLUXBUYFECUYLUXAUYEUWQUWSUYDUWRUWTV
      BUTVCVDVEVCVFVGVHUYCUYIUXTUARUYCUWNRJZMZUYIUXHAVIZKZINZAVJZUXLKZUXLUXMUXN
      UWTUOZAVJZOZHAPZMZGUXHPZIUFNZAVJZVKVLZUFUWNVMZQZVUDIVUIPZGUXHQZVNZFRPZUXT
      UYNUYIVUNUYNUYIMZVUIRJZUYOSJZVUDGUYOPZIVUIQZVUNUYMVUPUYCUYIUYMVUIUWNKVUPV
      UHUFUWNVOUWNVUIVPVQVRUWMVUQUYBUYMUYIUWMASJZVUQUWHCSJZUWJVUTUWHCBUQZUQZSDU
      WHVVBSJVVCSJBUDVSVVBSVSVGWFUWJVVAVUTACSVTTWAASWBVGWCUYCUYIVUSUYMUYCUYIMZV
      URIVUIUYQVUIJVVDUYQUWNJZUYRVKVLZMVURVUHVVFUFUYQUWNVUFUYQOVUGUYRVKVUFUYQAW
      DWEWGVVDVVEVVFVURVVFVVDVVEMZUXHUYRJZFWHZVURFUYRWIVVGVVIVURUYCUYIVVEVVIVUR
      LZUYIVVEMUYCUYQUYEOZECPZVVJUYHVVEVVLUWPUYGVVLUBUYQUWNUWQUYQOUYFVVKECUWQUY
      QUYEWJVCWKWLUYCVVLMVVHVURFUYCVVHVVLVURUYCVVHMZVVKVURECVVMUWRCJZMZVVKUXHUY
      EJZVURVVHVVKVVPLUYCVVNVVHUXHUYQJVVKVVPUYRUYQUXHUYQAWMWNUYQUYEUXHWQWOVRVVO
      VVPVVKVURVVMVVNVVPVVKVURLZLZUWMUYBVVHVVNVVRLZUWHUYBUWJVVHVVSUWHUYBMZUWJVV
      HMZMVVNVVPVVQVVTVVNVVPMVWAVVQUWHVVNUYBVVPVWAVVQLZUWHVVNMUYBVVPMZMUYEUXHUX
      NUWTUOZKZVWBUXNBCUWRUXHDWPUWHVWEVWBLVVNVWCUWHVWEVWAVVKVURUWHVWEVWAVNZVVKM
      ZVWDAVJZUYOJZUYRVWHKZVWHVUAOZHAPZVURVWIVWGVWIVWHAKVWDAWRVWHAVWDAUXHUXNUWT
      WSWTXAXBXRVWEUWHVVKVWJVWAVWEVVKMUYQVWDKZVWJVVKVWMVWEUYQUYEVWDXCXDUYQVWDAX
      EVGXIVWFVWLVVKVWAUWHVWLVWEVVHVWLUWJVVHUXHAJVWHVWHOZVWLUYRAUXHUYQAWRWNVVHV
      WHXFVWKVWNHUXHAUXMUXHOZVUAVWHVWHVWOUYTVWDAUXMUXHUXNUWTXGXHUTXJXKVHXLXMVUD
      VWJVWLMGVWHUYOUXLVWHOZUYSVWJVUCVWLUXLVWHUYRXNVWPVUBVWKHAUXLVWHVUAWJVCXOXJ
      XPXSXQUUAXTYAUUBXTYBYCYIYDYEYAYFYGYBYCYHUUCYHUUDYJVUDIGVUIUYOSFUUEUUFTUYN
      UYIVUNUXTLVUOVUMUXSFRVUOUXHRJZMZVUMUXKUXRVWRVUMUXIAOZUXKVWRVUMUXIAKZAUXIK
      ZMVWSVWRVUMVWTVXAVUMVWTLVWRUYPVUJVWTVULUYPUXIUYOULAUXHUYOUUGAUUJUUHUUIXRV
      WRVUMVXAVWRUYPVUJVXAVULVUOUYPVUJMZVXAVWQUYCUYIVXBVXAUYMVVDVXBMZUGAUXIVXCU
      GNZAJZVXDCJZVXDUXIJZUYCVXEVXFLZUYIVXBUWJVXHUWHUYBACVXDUUKVRXQVXCVXEVXFVXG
      LVXCVXEMZVXFVXDUHNZJZVXJUWNJZMZUHWHZVXGVVDVXFVXNLZVXBVXEUWPVXOUYCUYHUWPVX
      FVXDUWOJZVXNUWPVXPVXFUWOCVXDWQYKUHVXDUWNUULUUMUUNXQVXIVXMVXGUHVXIVXMVXJAV
      JZUXLKZVUCMZGUXHPZVXGVXBVXEVXMVXTLZVVDVUJVXEVYAUYPVUJVXEVXMVXTVXEVXMMVXJV
      UIJZVUJVXTVXLVXKVXEVYBVXKVXEMZVXLVXQVKVLZVYBVXDVXJAUUOVYBVXLVYDMVUHVYDUFV
      XJUWNVUFVXJOVUGVXQVKVUFVXJAWDWEWGUUPYGUUQVUEVXTIVXJVUIUYQVXJOZVUDVXSGUXHV
      YEUYSVXRVUCVYEUYRVXQUXLUYQVXJAWDUURUVAVCUUSUUTUVBWLWLVXEVXMVXTVXGLZLVXCVX
      EVXMVYFVXEVXKVYFVXLVXKVXEVYFVYCVXTVXDUXLJZGUXHPVXGVYCVXSVYGGUXHVYCVXRVUCV
      YGVYCVXRMVYGVUCVXRVYCVYGVYCVXRVXDVXQJVYGVXDVXJAUVKVXQUXLVXDUVCUVDYLUVEUVF
      UVGGVXDUXHUVHYMYLUVITVHYDYFYNTYDUVLUVJYJUVMTYOUXIAUVNYMVWRAUXJUXIUYNAUXJO
      ZUYIVWQUWMVYHUYBUYMBCADUVOZXQXQUTYTUYCVUMUXRLUYMUYIVWQUYCVUMUXRUYCUYPVULU
      XRVUJUYCUYPVULUXRUYCUYPMZVUKUXQGUXHVYJUXLUXHJZMZVUDUXQIVUIVYLVUCUXQUYSVYL
      VUCUXPHAPUXQVYLVUBUXPHAVYLUXMAJZMZUXPVUBVYNUXOVUAUXLVYJVYMUXOVUAOZVYKUYCV
      YMVYOUYPUWMVYMUYBVYOUXNABUWKCUXMDUWKYPUVPUVQYJYJUTYKYQVYLUXPHAUXJUWMVYHUY
      BUYPVYKVYIWCUVRYTUVSUWAUVTUWBUWCTWCYOYQTYDYEYNUWDUWHUWIUXGYRUWJEUABCUBUCD
      YSXMUWHUWLUYAYRZUWJUWHUWKUDJVYPBAUWEHFUWKUXJGUEUXJYPYSVGXMUWFTYIUWG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d m x r $.
    $( Define the class of bounded metrics.  A metric space is bounded iff it
       can be covered by a single ball. $)
    df-bnd $a |- Bnd = { m e. Met | A. x e. dom dom m E. r e. RR+
                                          dom dom m = ( x ( ball ` m ) r ) } $.
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd $p |- ( M e. Bnd <-> ( M e. Met /\ A. x e. X E. r e. RR+
                            X = ( x ( ball ` M ) r ) ) ) $=
      ( vm cv cdm cbl cfv co wceq crp wrex wral cme cbnd dmeq dmeqd syl6eqr
      fveq2 oveqd eqeq12d rexbidv raleqbidv df-bnd elrab2 ) FGZHZHZAGZDGZUHIJZK
      ZLZDMNZAUJOCUKULBIJZKZLZDMNZACOFBPQUHBLZUPUTAUJCVAUJBHZHCVAUIVBUHBRSETZVA
      UOUSDMVAUJCUNURVCVAUMUQUKULUHBIUAUBUCUDUEAFDUFUG $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M m x r $.  $d X m x r $.
    isbnd2.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd2 $p |- ( M e. Met -> ( M e. Bnd <->
                        A. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( cbnd wcel cme cv cbl cfv co wceq crp wrex wral isbnd baib ) BFGBHGCAIDI
      BJKLMDNOACPABCDEQR $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r s $.  $d X x y r s $.
    isbnd3.1 $e |- X = dom dom M $.
    $( The predicate "is a bounded metric space".  Uses a single point instead
       of an arbitrary point in the space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    isbnd3 $p |- ( ( M e. Bnd /\ X =/= (/) ) <-> ( M e. Met /\
                E. x e. X E. r e. RR+ X = ( x ( ball ` M ) r ) ) ) $=
      ( vy vs wcel wa cv co wceq crp wrex eqeq2d c2 wss wi a1i sylan2 cbnd wral
      c0 wne cme cbl isbnd anbi1i anass r19.2z ancoms oveq1 oveq2 cbvrex2v cmul
      cfv 2re 2pos elrpii rpmulcl mpan ad2antll ad2antrr cdiv cc0 rpcn 2cn 2ne0
      cc w3a divcan3 eqcomd syl3anc oveq2d biimpd adantr imp simpr eleq2 blhalf
      biimpac expr anasss anassrs eqsstrd syldan adantl blssm 3expa an32s eqssd
      rcla4ev syl2anc ex ralrimdva rexlimdvva syl5bi rexn0 jcad impbid2 pm5.32i
      3bitri ) BUAHZCUCUDZIBUEHZCAJZDJZBUFUPZKZLZDMNZACUBZIZXDIXEXLXDIZIXEXKACN
      ZIXCXMXDABCDEUGUHXEXLXDUIXEXNXOXEXNXOXDXLXOXKACUJUKXEXOXLXDXOCFJZGJZXHKZL
      ZGMNFCNXEXLXJXSCXPXGXHKZLADFGCMXFXPLXIXTCXFXPXGXHULOXGXQLXTXRCXGXQXPXHUMO
      UNXEXSXLFGCMXEXPCHZXQMHZIZIZXSXKACYDXFCHZIZXSXKYFXSIZPXQUOKZMHZCXFYHXHKZL
      ZXKYDYIYEXSYBYIXEYAPMHYBYIPUQURUSPXQUTVAZVBVCYGCYJYFXSCXPYHPVDKZXHKZLZCYJ
      QYFXSYOYDXSYORZYEYBYPXEYAYBXSYOYBXRYNCYBXQYMXPXHYBXQVIHZPVIHZPVEUDZXQYMLX
      QVFYRYBVGSYSYBVHSYQYRYSVJYMXQXQPVKVLVMVNOVOVBVPVQYFYOICYNYJYFYOVRYDYEYOYN
      YJQZYEYOIYDXFYNHZYTYOYEUUACYNXFVSWAYDUUAYTXEYAYBUUAYTRZYBXEYAIZYIUUBYLUUC
      YIUUAYTYHBCXPXFEVTWBTWCVQTWDWEWFYFYJCQZXSXEYEYCUUDYCXEYEIYIUUDYBYIYAYLWGX
      EYEYIUUDBXFYHCEWHWITWJVPWKXJYKDYHMXGYHLXIYJCXGYHXFXHUMOWLWMWNWOWPWQXOXDRX
      EXKACWRSWSWTXAXB $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x y r $.  $d X x y r $.  $d S x y r $.
    bndss.1 $e |- X = dom dom M $.
    $( A subset of a bounded metric space is bounded.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    bndss $p |- ( ( M e. Bnd /\ S C_ X ) -> ( M |` ( S X. S ) ) e. Bnd ) $=
      ( vy vr vx cme wcel cv cbl cfv co wceq crp wrex wral wa cin an32s wss cxp
      cres cdm metres ad2antrr metssba2 eleq2d biimpar adantllr wi ssel2 ancoms
      cbnd oveq1 eqeq2d rexbidv rcla4va sylan adantlll eqid an4s anassrs adantr
      blssp dfss biimpi incom syl6eq ineq1 sylan9eq adantll adantlr 3eqtr2rd ex
      reximdva imp syldan ralrimiva jca isbnd anbi1i 3imtr4i ) BHIZCEJZFJZBKLZM
      ZNZFOPZECQZRZACUAZRZBAAUBUCZHIZWOUDUDZGJZWFWOKLMZNZFOPZGWQQZRBUNIZWMRWOUN
      IWNWPXBWDWPWKWMBAUEUFWNXAGWQWNWRWQIZWRAIZXAWDWMXDXEWKWDWMRZXEXDXFAWQWRBCA
      DUGZUHUIUJWLXEWMXAWLXERWMXAWDXEWKWMXAUKWDXERZWKRWMXAXHWMWKXAXHWMRZWKCWRWF
      WGMZNZFOPZXAXEWMWKXLWDXEWMRWRCIZWKXLWMXEXMACWRULUMWJXLEWRCWEWRNZWIXKFOXNW
      HXJCWEWRWFWGUOUPUQURUSUTXIXLXAXIXKWTFOXIWFOIZRZXKWTXPXKRWSXJASZAWQXPWSXQN
      ZXKXHWMXOXRWDWMXEXOXRWFABWOCWRDWOVAVEVBVCVDXIXKAXQNZXOWMXKXSXHWMXKACASZXQ
      WMAACSZXTWMAYANACVFVGACVHVICXJAVJVKVLVMXIAWQNZXOXKWDWMYBXEXGVMUFVNVOVPVQV
      RTVOTVQTVRVSVTXCWLWMEBCFDWAWBGWOWQFWQVAWAWC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d M x r $.  $d Y x r $.  $d R x r $.
    blbnd.1 $e |- X = dom dom M $.
    $( A ball is bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (The
       proof was shortened by Mario Carneiro, 15-Jan-2014.) $)
    blbnd $p |- ( ( M e. Met /\ Y e. X /\ R e. RR+ ) ->
          ( M |` ( ( Y ( ball ` M ) R ) X. ( Y ( ball ` M ) R ) ) ) e. Bnd ) $=
      ( vx vr cme wcel crp cbl cfv co cdm cv wceq wrex cin sylanbrc eqid w3a c0
      cxp cres cbnd wne metres 3ad2ant1 blcntr wss simp1 blssm metssba2 syl2anc
      wa eleqtrd simp3 simp2 blres syl3anc syl6eq eqtr2d rcla4eov isbnd3 simpld
      elin inidm ) BHIZDCIZAJIZUAZBDABKLMZVLUCUDZUEIZVMNNZUBUFZVKVMHIZVOFOGOVMK
      LZMPGJQFVOQZVNVPUOVHVIVQVJBVLUGUHVKDVOIVJVODAVRMZPVSVKDVLVOBDACEUIZVKVHVL
      CUJVLVOPVHVIVJUKZBDACEULBCVLEUMUNZUPVHVIVJUQZVKVTVLVOVKVTVLVLRZVLVKVHDCVL
      RIZVJVTWEPWBVKVIDVLIWFVHVIVJURWADCVLVFSWDVMBDACVLEVMTUSUTVLVGVAWCVBFGVOJD
      AVOVRVCUTFVMVOGVOTVDSVE $.
      $( [15-Jan-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d M a b c d m r s t u v x y z $.  $d X a b c d m r s t u v x y z $.
    totbndbnd.1 $e |- X = dom dom M $.
    $( A totally bounded metric space is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    totbndbnd $p |- ( M e. TotBnd -> M e. Bnd ) $=
      ( vb vx va vc wcel wceq wrex wa crp c1 wi clt wbr c0 cr cc0 adantlr vv vd
      vy vr vu vs vz vt cme ctotbnd cbnd totbndmet cv cuni cbl cfv wral cfn 1rp
      oveq2 eqeq2d rexbidv ralbidv anbi2d rcla4v ax-mp wss w3a cvv simplr dmexg
      co cdm syl syl5eqel ad3antrrr simprr indexfi syl3anc wne crab cab abrexfi
      caddc cin dfrab3 inss2 eqsstri sylancl ad2antlr copab crn ssel2 metcl 1re
      ssfi readdcl 00id cle metge0 jctir 0re a1i leltadd syl22anc mpd syl5eqbrr
      lt01 elrp sylanbrc 3expa sylan2 anassrs rexlimdva 3ad2antr1 wn eqid eqeq1
      sylan wor syl2anc eleq2 ad2antrr ax-17 hban elrab ex adantl imp anasss wb
      rpre adantllr adantr an32s syld com23 elbl 3imtr4d syl5 syl5ibrcom abbidv
      eleq1 rnopab2 df-rab 3eqtr4g ovex dmopab2 eqeq1i dm0rn0 bitr3i rex0 rexeq
      pm4.71rd mtbiri ne0i unieq uni0 syl6eq syl5ib necon3d mpan9 r19.2z reximi
      eqidd rexlimivw sylbir necon2ai adantlll 3ad2antr2 eqnetrrd ssrab2 rpssre
      nsyl sstri ltso soss fimaxg mp3an1 ssrexv eluni2 biimpi syl6bir 3ad2antl2
      mp2 rcla4cva adantll hbre1 hbral hbra1 hb3an biimpa simplbi equcom syl5bi
      eqle idd necon3bd syl2anr imim12d pm2.61 sylsyld ralimdva anim1i peano2re
      ltle oveq1d rcla4ev mpan2 breq1 rcla4va ancom2s impl anim2i ltadd2 biimpd
      3ad2antl1 ltletr mettri 3exp2 imp41 lelttr mpand imdistand mp3an3 rexlimd
      exp3a exp4b blssm sseld eqrdv reximdva adantlrr mpdan ralrimdva istotbnd2
      impbid isbnd2 mpcom ) AUIHZAUJHZAUKHZAULUYTUAUMZUNZBIZDUMZEUMZUBUMZAUOUPZ
      VLZIZEBJZDVUCUQZKZUAURJZUBLUQZBUCUMZUDUMZVUIVLZIZUDLJZUCBUQVUAVUBUYTVUPVV
      AUCBVUPVUEVUFVUGMVUIVLZIZEBJZDVUCUQZKZUAURJZUYTVUQBHZKZVVAMLHZVUPVVGNUSVU
      OVVGUBMLVUHMIZVUNVVFUAURVVKVUMVVEVUEVVKVULVVDDVUCVVKVUKVVCEBVVKVUJVVBVUFV
      UHMVUGVUIUTVAVBVCVDVBVEVFVVIVVFVVAUAURVVIVUCURHZKZVVFVVAVVMVVFKZUEUMZBVGZ
      VVCEVVOJZDVUCUQZVVCDVUCJZEVVOUQZVHZUEURJZVVAVVNVVLBVIHZVVEVWBVVIVVLVVFVJU
      YTVWCVVHVVLVVFUYTBAVMZVMZVICUYTVWDVIHVWEVIHAUIVKVWDVIVKVNVOVPVVMVUEVVEVQV
      VCDEVUCBVIUEVRVSVVMVUEVWBVVAVVEVVIVUEVWBVVAVVLVVIVUEKZVWBVVAVWFVWAVVAUEUR
      VWFVVOURHZKZVWAVVAVWHVWAKZVURUFUMZVTZVWJVUROPZNZUFFUMZVUQGUMZAVLZMWDVLZIZ
      GVVOJZFLWAZUQZUDVWTJZVVAVWIVWTURHZVWTQVTZVXBVWGVXCVWFVWAVWGVWSFWBZURHVWTV
      XEVGVXCGFVVOVWQWCVWTLVXEWEVXEVWSFLWFLVXEWGWHVXEVWTWPWIWJVWIVWOVVOHZVWRKGF
      WKZWLZVWTQVWIVXEVWNLHZVWSKZFWBZVXHVWTVWHVVRVVPVXEVXKIZVVTVWFVVPVXLVWGVVIV
      VPVXLVUEVVIVVPKZVWSVXJFVXMVWSVXIVXMVWRVXIGVVOVVIVVPVXFVWRVXINZVVPVXFKVVIV
      WOBHZVXNVVOBVWOWMVVIVXOKVXIVWRVWQLHZUYTVVHVXOVXPUYTVVHVXOVHZVWQRHZSVWQOPV
      XPVXQVWPRHZMRHZVXRVUQVWOABCWNZWOVWPMWQWIVXQSSSWDVLZVWQOWRVXQSVWPWSPZSMOPZ
      KZVYBVWQOPZVXQVYCVYDVUQVWOABCWTXHXAVXQSRHZVYGVXSVXTVYEVYFNVYGVXQXBXCZVYHV
      YAVXTVXQWOXCSSVWPMXDXEXFXGVWQXIXJXKVWNVWQLUUCUUAXLXMXNUUNUUBTTXOGFVVOVWQU
      UDVWSFLUUEUUFVWHVVPVVRVXHQVTZVVTVWFVVRVYIVWGVVHVUEVVRVYIUYTVVHVUEKZVVRKZV
      XHQVXHQIZVVOQIZVYKXPVYMVXGVMZQIVYLVYNVVOQGFVVOVWQVXGVWPMWDUUGVXGXQUUHUUIV
      XGUUJUUKVYMVUGVUGIZEVVOJZVYKVYMVYPVYOEQJVYOEUULVYOEVVOQUUMUUOVYJVUCQVTZVV
      RVYPVVHBQVTVUEVYQBVUQUUPVUEVUCQBQVUCQIZVUDQIVUEBQIVYRVUDQUNQVUCQUUQUURUUS
      VUDBQXRUUTUVAUVBVYQVVRKVVQDVUCJVYPVVQDVUCUVCVVQVYPDVUCVVCVYOEVVOVVCVUGUVE
      UVDUVFVNXSUVNUVGUVHUVITUVJUVKVWTOXTZVXCVXDVXBVWTRVGROXTVYSVWTLRVWSFLUVLZU
      VMUVOUVPVWTROUVQUWEUDUFVWTOUVRUVSYAVXBVXAUDLJZVWIVVAVWTLVGVXBWUANVYTVXAUD
      VWTLUVTVFVWIVXAVUTUDLVWFVWAVURLHZVXAVUTNVWGVWFVWAKZWUBKZVXAVUTWUDVXAKZUGB
      VUSWUEUGUMZBHZWUFVUSHZWUEWUGWUFUHUMZHZUHVUCJZWUHWUCWUGWUKNZWUBVXAVUEWULVV
      IVWAVUEWUGWUFVUDHZWUKVUDBWUFYBWUMWUKUHWUFVUCUWAUWBUWCWJYCWUEWUJWUHUHVUCWU
      EWUIVUCHZKZWUIVVBIZEVVOJZWUJWUHNZWUDWUNWUQVXAWUCWUNWUQWUBVWAWUNWUQVWFVVRV
      VPWUNWUQVVTVVQWUQDWUIVUCVUFWUIIVVCWUPEVVOVUFWUIVVBXRVBUWFUWDUWGTTWUOWUPWU
      REVVOWUEWUNEWUDVXAEWUCWUBEVWFVWAEVWFEYDVVPVVRVVTEVVPEYDVVQEDVUCVUFVUCHEYD
      VVCEVVOUWHUWIVVSEVVOUWJUWKYEWUBEYDYEVXAEYDYEWUNEYDYEWUREYDWUOVUGVVOHZWUPW
      UJWUHWUPWUJKWUFVVBHZWUOWUSKZWUHWUPWUJWUTWUIVVBWUFYBUWLWVAWUGVUGWUFAVLZMOP
      ZKZWUGVUQWUFAVLZVUROPZKZWUTWUHWVAWUGWVCWVFWUEWUSWUGWVCWVFNZNZWUNWUEWUSKVU
      QVUGAVLZMWDVLZVURWSPZWVIWUDVXAWUSWVLWUCWUBVXAWUSKZWVLVWFVVRVVPWUBWVMKZWVL
      NZVVTVVIVVPWVOVUEVXMWVNWVLWVNVXMVWJVURWSPZUFVWTUQZWUSKZWVLWUBVXAWUSWVRWUB
      VXAKWVQWUSWUBVXAWVQWUBVWMWVPUFVWTVWJVWTHZWUBVWJLHZVWMWVPNWVSWVTVWJVWQIZGV
      VOJZVWSWWBFVWJLVWNVWJIVWRWWAGVVOVWNVWJVWQXRVBYFUWMWUBWVTKZVURVWJIZWVPNZVW
      MWWDXPZWVPNWVPWVTWWEWUBWVTVWJRHZWWEVWJYLZWWDVWJVURIZWWGWVPUDUFUWNWWGWWIWV
      PVWJVURUWPYGUWOVNYHWWCWWFVWKVWLWVPWWCWWDVURVWJWWCWWDUWQUWRWVTWWGVURRHZVWL
      WVPNWUBWWHVURYLZVWJVURUXFUWSUWTWWDWVPUXAUXBXLUXCYIUXDYJVXMWUSWVQWVLVXMWUS
      WVQWVLVXMWUSKZWVKVWTHZWVQWVLWWLWVKLHZWVKVWQIZGVVOJZWWMVVIVVPWUSWWNVVPWUSK
      VVIVUGBHZWWNVVOBVUGWMZUYTVVHWWQWWNUYTVVHWWQVHZWVKRHZSWVKOPWWNWWSWVJRHZWWT
      VUQVUGABCWNZWVJUXEVNWWSSVYBWVKOWRWWSSWVJWSPZVYDKZVYBWVKOPZWWSWXCVYDVUQVUG
      ABCWTXHXAWWSVYGVYGWXAVXTWXDWXENVYGWWSXBXCZWXFWXBVXTWWSWOXCSSWVJMXDXEXFXGW
      VKXIXJXKXLXMWUSWWPVXMWUSWVKWVKIZWWPWVKXQWWOWXGGVUGVVOVWOVUGIZVWQWVKWVKWXH
      VWPWVJMWDVWOVUGVUQAUTUXGVAUXHUXIYHVWSWWPFWVKLVWNWVKIVWRWWOGVVOVWNWVKVWQXR
      VBYFXJWVPWVLUFWVKVWTVWJWVKVURWSUXJUXKXSYJUXLXLYGTXOUXMXMWUDWUSWVLWVINZVXA
      WUCWUSWUBWXIWUCWUSKVWFWWQKZWUBWXIVWFVWAWUSWXJVWAWUSKZWWQVWFVVPVVRWUSWWQVV
      TWWRUXQZUXNXMVVIWWQWUBWXIVUEVVIWWQKZWUBKZWUGWVLWVHWXNWUGWVLWVHNWXNWUGKZWV
      CWVLWVFWXOWVCWVJWVBWDVLZWVKOPZWVLWVFNWXMWUGWVCWXQNWUBWXMWUGKZWVCWXQWXRWVB
      RHZVXTWXAWVCWXQYKUYTWWQWUGWXSVVHUYTWWQWUGWXSVUGWUFABCWNXKYMZVXTWXRWOXCWXM
      WXAWUGUYTVVHWWQWXAWXBXKZYNZWVBMWVJUXOVSUXPTWXOWXQWVLWVFWXOWXQWVLKZWXPVURO
      PZWVFWXOWXPRHZWWTWWJWYCWYDNWXOWXAWXSWYEWXMWXAWUBWUGWYAYCWXMWUGWXSWUBWXTTW
      VJWVBWQYAZWXMWUGWWTWUBWXRWXAVXTWWTWYBWOWVJMWQWITWUBWWJWXMWUGWWKWJZWXPWVKV
      URUXRVSWXOWVEWXPWSPZWYDWVFWXMWUGWYHWUBVVIWUGWWQWYHUYTVVHWUGWWQWYHUYTVVHWU
      GWWQWYHVUQWUFVUGABCUXSUXTUYAYOTWXOWVERHZWYEWWJWYHWYDKWVFNWXMWUGWYIWUBVVIW
      UGWYIWWQUYTVVHWUGWYIVUQWUFABCWNXKTTWYFWYGWVEWXPVURUYBVSUYCYPUYGYPYQYGYQYM
      XSYOTXFTUYDWUEWUSWUTWVDYKZWUNWUDWUSWYJVXAWUCWUSWYJWUBVWFVWAWUSWYJWXKVWFWW
      QWYJWXLVVIWWQWYJVUEUYTWWQWYJVVHUYTWWQVVJWYJUSWUFAVUGMBCYRUYETTXLXMTTTWUDW
      UHWVGYKZVXAWUNWUSVWFWUBWYKVWAVVIWUBWYKVUEUYTVVHWUBWYKWUFAVUQVURBCYRXKTTVP
      YSYTUYHUYFXFXNYPWUDWUHWUGNZVXAVWFWUBWYLVWAVVIWUBWYLVUEVVIWUBKVUSBWUFUYTVV
      HWUBVUSBVGAVUQVURBCUYIXKUYJTTYNUYQUYKYGYMUYLYTXFYGXNYIYMUYMUYNYGXNYTUYOEU
      AABDUBCUYPUCABUDCUYRYSUYS $.
      $( [15-Jan-2014] $) $( [2-Sep-2009] $)
  $}

