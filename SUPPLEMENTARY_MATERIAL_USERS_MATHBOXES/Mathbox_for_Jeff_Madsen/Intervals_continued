$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intervals (continued)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d R x y z $.  $d F k y z $.
    ismrer1.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    ismrer1.2 $e |- F = ( x e. RR |-> ( { 1 } X. { x } ) ) $.
    $( An isometry between ` RR ` and ` RR ^ 1 ` .  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    ismrer1 $p |- F e. ( R Ismty ( Rn ` 1 ) ) $=
      ( vy vz vk c1 cfv co wcel cr csn wceq ax-mp syl cmin c2 cexp crrn cmap cv
      cismty wf1o wral cme wa wb remet cn 1nn rrnmet remetba cdm cfz rrndm fzsn
      cz 1z oveq2i eqtr2i isismty mp2an wfn crn wi dff1o6 cxp cvv snex xpex a1i
      fnmpt mprg wrex cab cop elexi xpsn eqeq2i rexbii abbii rnmpt reex 3eqtr4i
      vex mapsn sneq xpeq2d fvmpt eqeqan12d wne snnz xp11 simprbi syl6bi rgen2a
      sneqr mpbir3an cabs csu csqr fveq1d snid fvconst2 syl6eq oveqan12d oveq1d
      c0 resubcl recnd sqcl eqeltrd fveq2 oveq12d fsum1 sylancr absresq 3eqtr4d
      cc fveq2d cc0 cle wbr abscl absge0 sqrsq syl2anc eqtr2d remetdval wf f1of
      feq3 mpbir ffvelrni rrnmval mp3an1 syl2an mpbir2an ) CBIUAJZUDKLZMMINZUBK
      ZCUEZFUCZGUCZBKZUUFCJZUUGCJZUUAKZOZGMUFFMUFZBUGLUUAUGLZUUBUUEUUMUHUIBDUJI
      UKLZUUNULIUMPFGCBUUAMUUDBDUNUUAUOUOZMIIUPKZUBKZUUDUUOUUPUUROULIUQPUUQUUCM
      UBIUSLZUUQUUCOUTIURPVAZVBVCVDUUECMVEZCVFZUUDOUUIUUJOZUUFUUGOZVGZGMUFFMUFF
      GMUUDCVHUUCAUCZNZVIZVJLZUVAAMAMUVHCVJEVNUVIUVFMLUUCUVGIVKZUVFVKVLVMVOUUFU
      VHOZAMVPZFVQUUFIUVFVRNZOZAMVPZFVQUVBUUDUVLUVOFUVKUVNAMUVHUVMUUFIUVFIUKULV
      SZAWGVTWAWBWCAFMUVHCEWDAMIFWEUVPWHWFUVEFGMUUFMLZUUGMLZUHZUVCUUCUUFNZVIZUU
      CUUGNZVIZOZUVDUVQUVRUUIUWAUUJUWCAUUFUVHUWAMCUVFUUFOUVGUVTUUCUVFUUFWIWJEUU
      CUVTUVJUUFVKVLWKZAUUGUVHUWCMCUVFUUGOUVGUWBUUCUVFUUGWIWJEUUCUWBUVJUUGVKVLW
      KZWLUWDUVTUWBOZUVDUWDUUCUUCOZUWGUUCXJWMUVTXJWMUWDUWHUWGUHUIIUVPWNUUFFWGZW
      NUUCUVTUUCUWBWOVDWPUUFUUGUWIWSQWQWRWTZUULFGMUVSUUFUUGRKZXAJZUUQHUCZUUIJZU
      WMUUJJZRKZSTKZHXBZXCJZUUHUUKUVSUWSUWLSTKZXCJZUWLUVSUWRUWTXCUVSIUUIJZIUUJJ
      ZRKZSTKZUWKSTKZUWRUWTUVSUXDUWKSTUVQUVRUXBUUFUXCUUGRUVQUXBIUWAJZUUFUVQIUUI
      UWAUWEXDIUUCLZUXGUUFOIUVPXEZUUCUUFIUWIXFPXGUVRUXCIUWCJZUUGUVRIUUJUWCUWFXD
      UXHUXJUUGOUXIUUCUUGIGWGXFPXGXHXIZUVSUUSUXEYALUWRUXEOUTUVSUXEUXFYAUXKUVSUW
      KYALZUXFYALUVSUWKUUFUUGXKZXLZUWKXMQXNUWQUXEHIUWMIOZUWPUXDSTUXOUWNUXBUWOUX
      CRUWMIUUIXOUWMIUUJXOXPXIXQXRUVSUWKMLUWTUXFOUXMUWKXSQXTYBUVSUWLMLZYCUWLYDY
      EZUXAUWLOUVSUXLUXPUXNUWKYFQUVSUXLUXQUXNUWKYGQUWLYHYIYJUUFUUGBDYKUVQUUIUUR
      LZUUJUURLZUUKUWSOZUVRMUURUUFCMUURCYLZMUUDCYLZUUEUYBUWJMUUDCYMPUURUUDOUYAU
      YBUIUUTUURUUDMCYNPYOZYPMUURUUGCUYCYPUUOUXRUXSUXTULHIUUIUUJYQYRYSXTWRYT $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d R x y z $.
    reheibor.1 $e |- R = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    reheibor.2 $e |- M = ( ( abs o. - ) |` ( Y X. Y ) ) $.
    reheibor.3 $e |- T = ( MetOpen ` M ) $.
    reheibor.4 $e |- U = ( MetOpen ` R ) $.
    $( Heine-Borel theorem for real numbers.  A subset of ` RR ` is compact iff
       it is closed and bounded.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    reheibor $p |- ( Y C_ RR -> ( T e. Comp <->
                                  ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( cr c1 cfv cres wcel co wb eqid cme ax-mp a1i vx vy vz wss crrn csn cmpt
      cv cxp cima copn ccmp ccld cbnd wa cn cfz cmap 1nn imassrn wf1o wceq wral
      crn wf cismty ismrer1 remet rrnmet remetba cdm rrndm eqcomi isismty mp2an
      mpbi simpli f1of frn sstri rrnheibor sylancr ctop chomeosm cabs cmin ccom
      cnmet metres eqeltri opntop pm3.2i id ismtyres syl22anc xpss12 anidms syl
      resabs1 reseq1i 3eqtr4g oveq1d eleqtrd ismtyhmeo chomeo wbr wne ne0i hmph
      wi c0 syl5ibr cmptoppr 3expa syldan cuni uniopn2 hmeocld ismtybnd syl3anc
      imp anbi12d 3bitr4d ) EJUDZKUELZUAJKUFUAUHUFUIUGZEUJZYGUIMZUKLZULNZYGYEUK
      LZUMLNZYHUNNZUOZBULNZECUMLNZDUNNZUOYDKUPNZYGJKKUQOUROZUDZYJYNPUSYTYDYGYFV
      DZYSYFEUTJYSYFVEZUUAYSUDJYSYFVAZUUBUUCUBUHZUCUHZAOUUDYFLUUEYFLYEOVBUCJVCU
      BJVCZYFAYEVFONZUUCUUFUOZUAAYFFYFQVGZARNZYERNZUUGUUHPAFVHZYRUUKUSKVISZUBUC
      YFAYEJYSAFVJZYEVKVKZYSYRUUOYSVBUSKVLSVMVNVOVPVQJYSYFVRSJYSYFVSSVTTYIYKYHK
      YSYGYSQYHQZYIQZYKQZWAWBYDBWCNZYIWCNZUOZYFEMZBYIWDOZNZYOYJPZUUSUUTDRNZUUSD
      WEWFWGZEEUIZMZRGUVGRNUVIRNUVGUVGQWHUVGEWISWJZDBHWKSYHRNZUUTUUKUVKUUMYEYGW
      ISZYHYIUUQWKSWLYDUVBDYHVFOZNZUVDYDUVBAUVHMZYHVFOZUVMYDUUJUUKUUGYDUVBUVPNU
      UJYDUULTUUKYDUUMTUUGYDUUITYDWMZEYGUVOYHYFAYEJUUNYGQUVOQUUPWNWOYDUVODYHVFY
      DUVGJJUIZMZUVHMZUVIUVODYDUVHUVRUDZUVTUVIVBYDUWAEJEJWPWQUVGUVHUVRWSWRAUVSU
      VHFWTGXAXBXCZUVFUVKUVNUVDXJUVJUVLUVBBYIDYHHUUQXDVOWRUVAUVDBYIXEXFZUVEUVAU
      VDUWCUVDUWCUVAUVCXKXGUVCUVBXHBYIXIXLYAUUSUUTUWCUVEBYIXMXNXOWBYDYPYLYQYMYD
      CWCNZYKWCNZYFCYKWDONZYDYPYLPUWDYDUUJUWDUULACIWKSTUWEYDUUKUWEUUMYEYKUURWKS
      TUWFYDUUGUWFUUIUUJUUKUUGUWFXJUULUUMYFCYKAYEIUURXDVOSTUVQEYFCYKJUUJJCXPVBU
      ULACJUUNIXQSXRWOYDUVFUVKUVNYQYMPUVFYDUVJTUVKYDUVLTUWBUVBDYHXSXTYBYC $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d J x y z $.  $d M x y z $.
    iccbnd.1 $e |- J = ( A [,] B ) $.
    iccbnd.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    $( A closed interval in ` RR ` is bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    iccbnd $p |- ( ( A e. RR /\ B e. RR ) -> M e. Bnd ) $=
      ( cr wcel wa co wceq cc cle wbr c1 caddc clt recn wb syl2anc vx vy vz cdm
      cv cbl cfv crp wrex wral cbnd cicc cabs cmin ccom cxp cres cme eqid cnmet
      wss iccssre ax-resscn syl6ss cnmetba metssba2 sylancr xpeq12i eqtri dmeqi
      reseq2i syl6eqr eleq2d w3a elicc2 cc0 resubcl ancoms 3syl readdcl sylancl
      abscl 1re a1i absge0 lt01 addgegt0 syl22anc elrp sylanbrc adantr 3ad2ant1
      cin ad2antlr ad2antrr syl3anc sselda cnmetdval syl2an 3ad2antr1 3ad2antl1
      elbl adantlr adantll cneg absle simplr2 simpr3 simpr1 simpllr leneg mpbid
      simplll renegcl syl simplr1 le2add mp2and ad3antrrr adantl negcl negsubdi
      wi addcom eqtr4d negsub 3brtr3d simpr2 simpll simp1 mpbir2and leabs letrd
      simplr3 ltp1 lelttrd eqbrtrd ex sylbid ax-mp ssrdv sseqin2 sylib syl6reqr
      imp ineq2i 3eqtr4rd syl5eqss biimpar syl6eleqr blssp oveq2 eqeq2d rcla4ev
      sylbird ralrimiv metres eqeltri isbnd2 sylibr ) AGHZBGHZIZDUDZUDZUAUEZUBU
      EZDUFUGZJZKZUBUHUIZUAUVEUJZDUKHZUVCUVKUAUVEUVCUVFUVEHUVFABULJZHZUVKUVCUVN
      UVEUVFUVCUVNUMUNUOZUVNUVNUPZUQZUDZUDZUVEUVCUVPURHZUVNLVAUVNUVTKUVPUVPUSZU
      TZUVCUVNGLABVBVCVDZUVPLUVNUVPUWBVEZVFVGZUVDUVSDUVRDUVPCCUPZUQZUVRFUWGUVQU
      VPCUVNCUVNEEVHVKVIVJVJZVLVMUVCUVOUVFGHZAUVFMNZUVFBMNZVNZUVKABUVFVOZUVCUWM
      UVKUVCUWMIZBAUNJZUMUGZOPJZUHHZUVEUVFUWRUVHJZKZUVKUVCUWSUWMUVCUWRGHZVPUWRQ
      NZUWSUVCUWQGHZOGHZUXBUVCUWPGHZUWPLHZUXDUVBUVAUXFBAVQVRZUWPRZUWPWBVSZWCUWQ
      OVTWAZUVCUXDUXEVPUWQMNZVPOQNZUXCUXJUXEUVCWCWDUVCUXFUXGUXLUXHUXIUWPWEVSUXM
      UVCWFWDUWQOWGWHUWRWIWJZWKZUWOUVEUVFUWRUVPUFUGJZCWMZUWTUWOUXPUVNWMZUVNUXQU
      VEUWOUVNUXPVAUXRUVNKUWOUCUVNUXPUWOUCUEZUVNHZUXSUXPHZUWOUXTIZUYAUXSLHZUVFU
      XSUVPJZUWRQNZUYBUWAUVFLHZUWSUYAUYCUYEISUWAUYBUWCWDUWMUYFUVCUXTUWJUWKUYFUW
      LUVFRZWLZWNUVCUWSUWMUXTUXNWOUXSUVPUVFUWRLUWEXBWPUVCUXTUYCUWMUVCUVNLUXSUWD
      WQXCUWOUXTUYEUWOUXTUXSGHZAUXSMNZUXSBMNZVNZUYEUVCUXTUYLSUWMABUXSVOWKUWOUYL
      UYEUWOUYLIZUYDUVFUXSUNJZUMUGZUWRQUWMUYLUYDUYOKZUVCUWJUWKUYLUYPUWLUWJUYJUY
      IUYPUYKUWJUYFUYCUYPUYIUYGUXSRZUVFUXSUVPUWBWRWSWTXAXDUYMUYOUWQUWRUWMUYLUYO
      GHZUVCUWJUWKUYLUYRUWLUWJUYJUYIUYRUYKUWJUYIIUYNGHZUYNLHUYRUVFUXSVQZUYNRUYN
      WBVSWTXAXDZUVCUXDUWMUYLUXJWOZUVCUXBUWMUYLUXKWOUYMUYOUWPUWQVUAUVCUXFUWMUYL
      UXHWOZVUBUYMUYOUWPMNZUWPXEZUYNMNZUYNUWPMNZUYMUYSUXFVUDVUFVUGISUWMUYLUYSUV
      CUWJUWKUYLUYSUWLUWJUYJUYIUYSUYKUYTWTXAXDVUCUYNUWPXFTUYMABXEZPJZUVFUXSXEZP
      JZVUEUYNMUYMUWKVUHVUJMNZVUIVUKMNZUWJUWKUWLUVCUYLXGUYMUYKVULUWOUYIUYJUYKXH
      UYMUYIUVBUYKVULSUWOUYIUYJUYKXIZUVAUVBUWMUYLXJZUXSBXKTXLUYMUVAVUHGHZUWJVUJ
      GHZUWKVULIVUMYCUVAUVBUWMUYLXMUYMUVBVUPVUOBXNXOUWJUWKUWLUVCUYLXPZUYMUYIVUQ
      VUNUXSXNXOZAVUHUVFVUJXQWHXRUYMVUIVUHAPJZVUEUYMALHZVUHLHZVUIVUTKUVAVVAUVBU
      WMUYLARXSZUYMBLHZVVBUVCVVDUWMUYLUVBVVDUVABRXTWOZBYAXOAVUHYDTUYMVVDVVAVUEV
      UTKVVEVVCBAYBTYEUYMUYFUYCVUKUYNKUWMUYFUVCUYLUYHWNUYLUYCUWOUYIUYJUYCUYKUYQ
      WLXTUVFUXSYFTZYGUYMVUKBAXEZPJZUYNUWPMUYMUWLVUJVVGMNZVUKVVHMNZUWJUWKUWLUVC
      UYLYNUYMUYJVVIUWOUYIUYJUYKYHUWOUVAUYIUYJVVISUYLUVAUVBUWMYIUYIUYJUYKYJAUXS
      XKWSXLUYMUWJVUQUVBVVGGHZUWLVVIIVVJYCVURVUSVUOUVAVVKUVBUWMUYLAXNXSUVFVUJBV
      VGXQWHXRVVFUYMVVDVVAVVHUWPKVVEVVCBAYFTYGYKUYMUXFUWPUWQMNVUCUWPYLXOYMUYMUX
      DUWQUWRQNVUBUWQYOXOYPYQYRYSUUEYKYRUUAUVNUXPUUBUUCUXQUXRKUWOCUVNUXPEUUFWDU
      VCUVEUVNKUWMUVCUVNUVTUVEUWFUWIUUDWKUUGUWOUWACLVAZUVFCHUWSUWTUXQKUWAUWOUWC
      WDUVCVVLUWMUVCCUVNLEUWDUUHWKUWOUVFUVNCUVCUVOUWMUWNUUIEUUJUXOUWRCUVPDLUVFU
      WEFUUKWHYEUVJUXAUBUWRUHUVGUWRKUVIUWTUVEUVGUWRUVFUVHUULUUMUUNTYRYSUUOUUPDU
      RHUVMUVLSDUWHURFUWAUWHURHUWCUVPCUUQYTUURUADUVEUBUVEUSUUSYTUUT $.
      $( [2-Sep-2009] $)
  $}

  ${
    icccmpALT.1 $e |- J = ( A [,] B ) $.
    icccmpALT.2 $e |- M = ( ( abs o. - ) |` ( J X. J ) ) $.
    icccmpALT.3 $e |- T = ( MetOpen ` M ) $.
    $( A closed interval in ` RR ` is compact.  Alternate proof of ~ icccmp
       using the Heine-Borel theorem ~ heibor .  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       14-Aug-2014.) $)
    icccmpALT $p |- ( ( A e. RR /\ B e. RR ) -> T e. Comp ) $=
      ( cr wcel wa ccmp cioo crn ctg cfv ccld cbnd wss eqid wb cicc co syl5eqss
      iccssre cabs cmin ccom cxp cres tgioo reheibor syl icccld syl5eqel iccbnd
      copn mpbir2and ) AIJBIJKZCLJZDMNOPZQPZJZERJZUSDISUTVCVDKUAUSDABUBUCZIFABU
      EUDUFUGUHIIUIUJZCVAEDVFTZGHVFVFUQPZVGVHTUKULUMUSDVEVBFABUNUOABDEFGUPUR $.
      $( [14-Aug-2014] $) $( [2-Sep-2009] $)
  $}


