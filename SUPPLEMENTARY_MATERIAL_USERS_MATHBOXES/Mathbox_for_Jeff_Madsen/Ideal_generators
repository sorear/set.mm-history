$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ideal generators
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c IdlGen $.

  $( Extend class notation with the ideal generation function. $)
  cigen $a class IdlGen $.

  ${
    $d r s i j $.
    $( Define the ideal generated by a subset of a ring. $)
    df-igen $a |- IdlGen = ( r e. RingOps , s e. ~P ran ( 1st ` r ) |->
                      |^| { j e. ( Idl ` r ) | s C_ j } ) $.
  $}

  ${
    $d R r s i j $.  $d S r s i j $.  $d X r s i j $.  $d I j $.
    igenval.1 $e |- G = ( 1st ` R ) $.
    igenval.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       20-Dec-2013.) $)
    igenval $p |- ( ( R e. RingOps /\ S C_ X ) -> ( R IdlGen S ) =
                                        |^| { j e. ( Idl ` R ) | S C_ j } ) $=
      ( vr vs crngo wcel wss cv cidl cfv crab cint cvv wceq c1st cigen co wa c0
      wne wrex rngoidl sseq2 rcla4ev sylan rabn0 sylibr intex sylib cpw eqeltri
      crn fvex rnex elpw2 simpl fveq2d wb sseq1 adantl rabeqbidv inteqd syl6eqr
      fveq2 rneqd pweqd df-igen ovmpt2x syl3an2br mpd3an3 ) AJKZBELZBCMZLZCANOZ
      PZQZRKZABUAUBWBSZVPVQUCZWAUDUEZWCWEVSCVTUFZWFVPEVTKVQWGADEFGUGVSVQCEVTVRE
      BUHUIUJVSCVTUKULWAUMUNVQVPBEUOZKWCWDBEEDUQZRGDDATOZRFATURUPUSUPUTHIABJHMZ
      TOZUQZUOIMZVRLZCWKNOZPZQWBUARWHWKASZWNBSZUCZWQWAWTWOVSCWPVTWTWKANWRWSVAVB
      WSWOVSVCWRWNBVRVDVEVFVGWRWMEWRWMWIEWRWLDWRWLWJDWKATVIFVHVJGVHVKCIHVLVMVNV
      O $.
      $( [20-Dec-2013] $) $( [10-Jun-2010] $)

    $( A set is a subset of the ideal it generates.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenss $p |- ( ( R e. RingOps /\ S C_ X ) -> S C_ ( R IdlGen S ) ) $=
      ( vj crngo wcel wss wa cv cidl cfv crab cint cigen co ssintub igenval
      syl5sseqr ) AHIBDJKBGLJGAMNZOPBABQRGBUBSABGCDEFTUA $.
      $( [10-Jun-2010] $)

    $( The ideal generated by a set is an ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenidl $p |- ( ( R e. RingOps /\ S C_ X ) ->
                              ( R IdlGen S ) e. ( Idl ` R ) ) $=
      ( vj crngo wcel wss wa cigen co cv cidl cfv crab cint igenval c0 wne wrex
      rngoidl sseq2 rcla4ev sylan sylibr ssrab2 intidl mp3an3 syldan eqeltrd
      rabn0 ) AHIZBDJZKZABLMBGNZJZGAOPZQZRZUSABGCDEFSUNUOUTTUAZVAUSIZUPURGUSUBZ
      VBUNDUSIUOVDACDEFUCURUOGDUSUQDBUDUEUFURGUSUMUGUNVBUTUSJVCURGUSUHUTAUIUJUK
      UL $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j $.  $d S j $.  $d I j $.
    $( The ideal generated by a set is the minimal ideal containing that set.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    igenmin $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) /\ S C_ I )
                                              -> ( R IdlGen S ) C_ I ) $=
      ( vj crngo wcel cidl cfv wss w3a cigen co cv crab cint wceq c1st crn eqid
      idlss wa sstr ancoms igenval anassrs syldanl 3impa sseq2 intminss 3adant1
      sylan2 eqsstrd ) AEFZCAGHZFZBCIZJABKLZBDMZIZDUNNOZCUMUOUPUQUTPZUMUOCAQHZR
      ZIZUPVAAVBCVCVBSZVCSZTUMVDUPVAVDUPUAUMBVCIZVAUPVDVGBCVCUBUCABDVBVCVEVFUDU
      KUEUFUGUOUPUTCIUMUSUPDCUNURCBUHUIUJUL $.
      $( [10-Jun-2010] $)

    $( The ideal generated by an ideal is that ideal.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    igenidl2 $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                                          -> ( R IdlGen I ) = I ) $=
      ( vj crngo wcel cidl cfv wa cigen co cv wss crab cint c1st crn wceq idlss
      eqid igenval syldan intmin adantl eqtrd ) ADEZBAFGZEZHABIJZBCKLCUFMNZBUEU
      GBAOGZPZLUHUIQAUJBUKUJSZUKSZRABCUJUKULUMTUAUGUIBQUECBUFUBUCUD $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R i j $.  $d S i j $.  $d I j $.  $d X i $.
    igenval2.1 $e |- G = ( 1st ` R ) $.
    igenval2.2 $e |- X = ran G $.
    $( The ideal generated by a subset of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    igenval2 $p |- ( ( R e. RingOps /\ S C_ X ) -> ( ( R IdlGen S ) = I <->
                                  ( I e. ( Idl ` R ) /\ S C_ I /\
                          A. j e. ( Idl ` R ) ( S C_ j -> I C_ j ) ) ) ) $=
      ( vi wcel wss wa wceq cv wi wral w3a igenmin adantr sseq2 crngo cigen cfv
      co cidl igenidl igenss 3expia ralrimiva 3jca eleq1 sseq1 imbi2d 3anbi123d
      ralbidv syl5ibcom 3adant3r3 adantlr crab cint ralrab biimpri ssint sylibr
      3ad2ant3 adantl igenval sseqtr4d eqssd ex impbid ) AUAJZBFKZLZABUBUDZEMZE
      AUEUCZJZBEKZBCNZKZEVTKZOZCVQPZQZVNVOVQJZBVOKZWAVOVTKZOZCVQPZQVPWEVNWFWGWJ
      ABDFGHUFABDFGHUGVLWJVMVLWICVQVLVTVQJWAWHABVTRUHUISUJVPWFVRWGVSWJWDVOEVQUK
      VOEBTVPWIWCCVQVPWHWBWAVOEVTULUMUOUNUPVNWEVPVNWELZVOEVLWEVOEKZVMVLVRVSWLWD
      ABERUQURWKEBINZKZIVQUSZUTZVOWEEWPKZVNWDVRWQVSWDWBCWOPZWQWRWDWNWAWBCIVQWMV
      TBTVAVBCEWOVCVDVEVFVNVOWPMWEABIDFGHVGSVHVIVJVK $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R j x y u v w r s $.  $d X j x y u v w r s $.  $d G x y r s $.
    $d H j x y u v w r s $.  $d A j x y u v w r s $.
    prnc.1 $e |- G = ( 1st ` R ) $.
    prnc.2 $e |- H = ( 2nd ` R ) $.
    prnc.3 $e |- X = ran G $.
    $( A principal ideal (an ideal generated by one element) in a commutative
       ring.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    prnc $p |- ( ( R e. CRing /\ A e. X ) -> ( R IdlGen { A } ) =
                                  { x e. X | E. y e. X x = ( y H A ) } ) $=
      ( vv vw wcel wa co cv wceq wrex wral oveq1 vj vu vr ccring csn cigen crab
      vs cidl cfv wss wi w3a cgi crngo crngorngo ssrab2 a1i eqid rngo0cl adantr
      rngolz eqcomd eqeq2d rcla4ev syl2anc eqeq1 rexbidv elrab sylanbrc cbvrexv
      syl6bb rngodir 3exp2 imp42 rngogcl 3expib imdistani rngocl 3expa ad2antlr
      mpan2 sylan eqeltrrd an32s anassrs oveq2 eleq1d syl5ibrcom adantld syl5bi
      rexlimdva ralrimiv rngoass anass1rs ralrimiva jca ralbidv anbi12d 3jca wb
      isidlc mpbird simpr c1st rneqi eqtri rngo1cl rngolidm snssd snssg biimpar
      crn idllmulcl eleq1 rabss sylibr ex syl5 expdimp snssi igenval2 syl2an )
      DUDMZCGMZNZDCUEZUFOAPZBPZCFOZQZBGRZAGUGZQZYMDUIUJZMZYGYMUKZYGUAPZUKZYMYRU
      KZULZUAYOSZUMZYFYPYQUUBYFYPYMGUKZEUNUJZYMMZUBPZKPZEOZYMMZKYMSZLPZUUGFOZYM
      MZLGSZNZUBYMSZUMZYDDUOMZYEUURDUPZUUSYENZUUDUUFUUQUUDUVAYLAGUQURUVAUUEGMZU
      UEYJQZBGRZUUFUUSUVBYEDEGUUEHJUUEUSZUTVAZUVAUVBUUEUUECFOZQZUVDUVFUVAUVGUUE
      CDEFGUUEUVEJHIVBVCUVCUVHBUUEGYIUUEQYJUVGUUEYIUUECFTVDVEVFYLUVDAUUEGYHUUEQ
      YKUVCBGYHUUEYJVGVHVIVJUVAUUPUBYMUUGYMMUUGGMZUUGUCPZCFOZQZUCGRZNUVAUUPYLUV
      MAUUGGYHUUGQZYLUUGYJQZBGRUVMUVNYKUVOBGYHUUGYJVGVHUVOUVLBUCGYIUVJQYJUVKUUG
      YIUVJCFTVDVKVLVIUVAUVMUUPUVIUVAUVLUUPUCGUVAUVJGMZNZUUPUVLUVKUUHEOZYMMZKYM
      SZUULUVKFOZYMMZLGSZNUVQUVTUWCUVQUVSKYMUUHYMMUUHGMZUUHUHPZCFOZQZUHGRZNUVQU
      VSYLUWHAUUHGYHUUHQZYLUUHYJQZBGRUWHUWIYKUWJBGYHUUHYJVGVHUWJUWGBUHGYIUWEQYJ
      UWFUUHYIUWECFTVDVKVLVIUVQUWHUVSUWDUVQUWGUVSUHGUVQUWEGMZNUVSUWGUVKUWFEOZYM
      MZUVAUVPUWKUWMUUSUVPUWKNZYEUWMUUSUWNNZYENUVJUWEEOZCFOZUWLYMUUSUVPUWKYEUWQ
      UWLQZUUSUVPUWKYEUWRUVJUWECDEFGHIJVMVNVOUWOUUSUWPGMZNZYEUWQYMMZUUSUWNUWSUU
      SUVPUWKUWSUVJUWEDEGHJVPVQVRUWTYENUWQGMZUWQYJQZBGRZUXAUUSUWSYEUXBUWPCDEFGH
      IJVSVTUWSUXDUUSYEUWSUWQUWQQZUXDUWQUSUXCUXEBUWPGYIUWPQYJUWQUWQYIUWPCFTVDVE
      WBWAYLUXDAUWQGYHUWQQYKUXCBGYHUWQYJVGVHVIVJWCWDWEWFUWGUVRUWLYMUUHUWFUVKEWG
      WHWIWLWJWKWMUVQUWBLGUVAUULGMZUVPUWBUVAUXFUVPNZNUULUVJFOZCFOZUWAYMUUSUXGYE
      UXIUWAQZUUSUXFUVPYEUXJUUSUXFUVPYEUXJUULUVJCDEFGHIJWNVNVOWEUUSUXGYEUXIYMMZ
      UUSUXGNUUSUXHGMZNZYEUXKUUSUXGUXLUUSUXFUVPUXLUULUVJDEFGHIJVSVQVRUXMYENUXIG
      MZUXIYJQZBGRZUXKUUSUXLYEUXNUXHCDEFGHIJVSVTUXLUXPUUSYEUXLUXIUXIQZUXPUXIUSU
      XOUXQBUXHGYIUXHQYJUXIUXIYIUXHCFTVDVEWBWAYLUXPAUXIGYHUXIQYKUXOBGYHUXIYJVGV
      HVIVJWCWEWDWOWPWQUVLUUKUVTUUOUWCUVLUUJUVSKYMUVLUUIUVRYMUUGUVKUUHETWHWRUVL
      UUNUWBLGUVLUUMUWAYMUUGUVKUULFWGWHWRWSWIWLWJWKWMWTWCYDYPUURXAYEUBKLDEFYMGU
      UEHIJUVEXBVAXCYFCYMYFYECYJQZBGRZCYMMYDYEXDYDUUSYEUXSUUTUVAFUNUJZGMZCUXTCF
      OZQZUXSUUSUYAYEDUXTFGGEXMDXEUJZXMJEUYDHXFXGZIUXTUSZXHVAUVAUYBCCDUXTFGIUYE
      UYFXIVCUXRUYCBUXTGYIUXTQYJUYBCYIUXTCFTVDVEVFWCYLUXSACGYHCQYKUXRBGYHCYJVGV
      HVIVJXJYDUUSYEUUBUUTUVAUUAUAYOUUSYRYOMZYEUUAUUSUYGNZYEYSYTYEYSNCYRMZUYHYT
      YEUYIYSCYRGXKXLUYHUYIYTUYHUYINZYLYHYRMZULZAGSYTUYJUYLAGUYJUYLYHGMUYJYKUYK
      BGUYJYIGMZNUYKYKYJYRMZUYHUYIUYMUYNCYIDEFYRGHIJXNWFYHYJYRXOWIWLVAWPYLAGYRX
      PXQXRXSXTWEWPWCWTYDUUSYGGUKYNUUCXAYEUUTCGYADYGUAEYMGHJYBYCXC $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d K x y z $.  $d X x y z $.  $d G y z $.  $d H x y z $.  $d U x y z $.
    $d Z x y $.
    isfldidl.1 $e |- G = ( 1st ` K ) $.
    isfldidl.2 $e |- H = ( 2nd ` K ) $.
    isfldidl.3 $e |- X = ran G $.
    isfldidl.4 $e |- Z = ( Id ` G ) $.
    isfldidl.5 $e |- U = ( Id ` H ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    isfldidl $p |- ( K e. Fld <-> ( K e. CRing /\
                                U =/= Z /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( vy vx vz wcel wceq syl cv wrex wa cfld wne cidl cfv csn cpr w3a fldcrng
      ccring cdrng flddivrng dvrunz divrngidl 3jca crngo co cdif wral crngorngo
      3ad2ant1 simp2 crab crn c1st rneqi eqtri rngo1cl ad2antrr cigen eldif wss
      wn snssi igenss sylan2 vex biimpri eleq2 syl5ibcom con3d imp sylan anasss
      snss sylan2b adantlr wo eldifi snssd igenidl an32s ovex sylib ord sylanl1
      elpr mpd eqtr3d eleqtrd eqeq1 rexbidv elrab simprd eqcom rexbii ralrimiva
      prnc sylibr 3adant2 jca32 isdrngo3 simp1 isfld2 sylanbrc impbii ) DUAOZDU
      IOZAFUBZDUCUDZFUEZEUFZPZUGZXPXQXRYBDUHXPDUJOZXRDUKZDABCEFGHIJKULQXPYDYBYE
      DBCEFGHIJUMQUNYCYDXQXPYCDUOOZXRLRMRZCUPZAPZLESZMEXTUQZURZTTYDYCYFXRYLXQXR
      YFYBDUSZUTXQXRYBVAXQYBYLXRXQYBTZYJMYKYNYGYKOZTZAYHPZLESZYJYPAEOZYRYPANRZY
      HPZLESZNEVBZOYSYRTYPAEUUCXQYSYBYOXQYFYSYMDACEEBVCDVDUDZVCIBUUDGVEVFHKVGQV
      HYPDYGUEZVIUPZEUUCXQYFYBYOUUFEPZYMYFYBTYOTZUUFXTPZVLZUUGYFYOUUJYBYOYFYGEO
      ZYGXTOZVLZTUUJYGEXTVJYFUUKUUMUUJYFUUKTUUEUUFVKZUUMUUJUUKYFUUEEVKZUUNYGEVM
      DUUEBEGIVNVOUUNUUMUUJUUNUUIUULUUNYGUUFOZUUIUULUUPUUNYGUUFMVPWDVQUUFXTYGVR
      VSVTWAWBWCWEWFUUHUUIUUGUUHUUFYAOZUUIUUGWGYFYOYBUUQYFYOTZYBUUQUURUUFXSOZYB
      UUQYOYFUUOUUSYOYGEYGEXTWHZWIDUUEBEGIWJVOXSYAUUFVRVSWAWKUUFXTEDUUEVIWLWPWM
      WNWQWOXQYOUUFUUCPZYBYOXQUUKUVAUUTNLYGDBCEGHIXGVOWFWRWSUUBYRNAEYTAPUUAYQLE
      YTAYHWTXAXBWMXCYIYQLEYHAXDXEXHXFXIXJMLDABCEFGHJIKXKXHXQXRYBXLDXMXNXO $.
      $( [10-Jun-2010] $)
  $}

  ${
    isfldidl2.1 $e |- G = ( 1st ` K ) $.
    isfldidl2.2 $e |- H = ( 2nd ` K ) $.
    isfldidl2.3 $e |- X = ran G $.
    isfldidl2.4 $e |- Z = ( Id ` G ) $.
    $( Determine if a ring is a field based on its ideals.  (Contributed by
       Jeff Madsen, 6-Jan-2011.) $)
    isfldidl2 $p |- ( K e. Fld <-> ( K e. CRing /\
                              X =/= { Z } /\ ( Idl ` K ) = { { Z } , X } ) ) $=
      ( cfld wcel ccring cgi cfv wne cidl wceq w3a wa 3anass csn cpr eqid crngo
      isfldidl wb crngorngo eqcom 0rngo syl5bb necon3bid anbi1d pm5.32i 3bitr4i
      syl bitri ) CJKCLKZBMNZEOZCPNEUAZDUBQZRZUQDUTOZVARZURABCDEFGHIURUCZUEUQUS
      VASZSUQVCVASZSVBVDUQVFVGUQUSVCVAUQUREDUTUQCUDKZUREQZDUTQZUFCUGVIEURQVHVJU
      REUHCURABDEFGHIVEUIUJUOUKULUMUQUSVATUQVCVATUNUP $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d R a b x y r s $.  $d P a b x y r s $.  $d X a b x y r s $.
    $d G x y r s $.  $d H a b x y r s $.
    ispridlc.1 $e |- G = ( 1st ` R ) $.
    ispridlc.2 $e |- H = ( 2nd ` R ) $.
    ispridlc.3 $e |- X = ran G $.
    $( The predicate "is a prime ideal".  Alternate definition for commutative
       rings.  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ispridlc $p |- ( R e. CRing -> ( P e. ( PrIdl ` R ) <-> ( P e. ( Idl ` R )
                              /\ P =/= X /\ A. a e. X A. b e. X
                      ( ( a H b ) e. P -> ( a e. P \/ b e. P ) ) ) ) ) $=
      ( vx vy vs vr wcel co wi wral wss wa ccring cpridl cfv cidl wne cv wo w3a
      crngo wb crngorngo ispridl syl cigen snssi igenidl syl2an adantrr adantrl
      csn wceq raleq orbi1d imbi12d ralbidv orbi2d rcla42v syl2anc adantlr wrex
      sseq1 crab cab prnc df-rab syl6eq abeq2d anbi12d adantr reeanv anbi2i an4
      bitri crngm4 3com23 3expa adantllr rngocl syl3an1 3expb idllmulcl sylanl1
      anassrs syldan oveq12 eleq1d syl5ibrcom rexlimdvva adantld syl5bir sylbid
      eqeltrrd ralrimivv ex igenss vex snss sylibr ssel syl5com orim12d imim12d
      syld ralrimdvva adantrd imdistand df-3an 3imtr4g ispridl2 impbid ) BUAOZA
      BUBUCOZABUDUCZOZAEUEZFUFZGUFZDPZAOZYFAOZYGAOZUGZQZGERFERZUHZYAYBYDYEKUFZL
      UFZDPZAOZLMUFZRZKNUFZRZUUBASZYTASZUGZQZMYCRNYCRZUHZYOYABUIOZYBUUIUJBUKZKL
      ABCDENMHIJULUMYAYDYETZUUHTUULYNTUUIYOYAUULUUHYNYAYDUUHYNQZYEYAYDUUMYAYDTZ
      UUHYMFGEEUUNYFEOZYGEOZTZTZUUHYSLBYGUTZUNPZRZKBYFUTZUNPZRZUVCASZUUTASZUGZQ
      ZYMYAUUQUUHUVHQZYDYAUUQTZUVCYCOZUUTYCOZUVIYAUUOUVKUUPYAUUJUVBESZUVKUUOUUK
      YFEUOZBUVBCEHJUPUQURYAUUPUVLUUOYAUUJUUSESZUVLUUPUUKYGEUOZBUUSCEHJUPUQUSUU
      GUVHUUAKUVCRZUVEUUEUGZQNMUVCUUTYCYCUUBUVCVAZUUCUVQUUFUVRUUAKUUBUVCVBUVSUU
      DUVEUUEUUBUVCAVKVCVDYTUUTVAZUVQUVDUVRUVGUVTUUAUVAKUVCYSLYTUUTVBVEUVTUUEUV
      FUVEYTUUTAVKVFVDVGVHVIUURYIUVDUVGYLUURYIUVDUURYITZYSKLUVCUUTUWAYPUVCOZYQU
      UTOZTZYPEOZYPUUBYFDPZVAZNEVJZTZYQEOZYQYTYGDPZVAZMEVJZTZTZYSUURUWDUWOUJZYI
      YAUUQUWPYDUVJUWBUWIUWCUWNYAUUOUWBUWIUJUUPYAUUOTZUWIKUVCUWQUVCUWHKEVLUWIKV
      MKNYFBCDEHIJVNUWHKEVOVPVQURYAUUPUWCUWNUJUUOYAUUPTZUWNLUUTUWRUUTUWMLEVLUWN
      LVMLMYGBCDEHIJVNUWMLEVOVPVQUSVRVIVSUWOUWEUWJTZUWGUWLTZMEVJNEVJZTZUWAYSUXB
      UWSUWHUWMTZTUWOUXAUXCUWSUWGUWLNMEEVTWAUWEUWJUWHUWMWBWCUWAUXAYSUWSUWAUWTYS
      NMEEUWAUUBEOZYTEOZTZTZYSUWTUWFUWKDPZAOUXGUUBYTDPZYHDPZUXHAUURUXFUXJUXHVAZ
      YIYAUUQUXFUXKYDYAUUQUXFUXKYAUXFUUQUXKUUBYTYFYGBCDEHIJWDWEWFWGVIUUNYIUXFUX
      JAOZUUQUUNYITUXFUXIEOZUXLUUNUXFUXMYIYAUXFUXMYDYAUXDUXEUXMYAUUJUXDUXEUXMUU
      KUUBYTBCDEHIJWHWIWJVIVIUUNYIUXMUXLYAUUJYDYIUXMTUXLUUKYHUXIBCDAEHIJWKWLWMW
      NWGXBUWTYRUXHAYPUWFYQUWKDWOWPWQWRWSWTXAXCXDYAUUQUVGYLQYDUVJUVEYJUVFYKUVJY
      FUVCOZUVEYJYAUUOUXNUUPUWQUVBUVCSZUXNYAUUJUVMUXOUUOUUKUVNBUVBCEHJXEUQYFUVC
      FXFXGXHURUVCAYFXIXJUVJYGUUTOZUVFYKYAUUPUXPUUOUWRUUSUUTSZUXPYAUUJUVOUXQUUP
      UUKUVPBUUSCEHJXEUQYGUUTGXFXGXHUSUUTAYGXIXJXKVIXLXMXNXDXOXPYDYEUUHXQYDYEYN
      XQXRXAYAUUJYOYBQUUKUUJYOYBABCDEFGHIJXSXDUMXT $.
      $( [19-Jun-2010] $)

    $d A a b $.  $d B b $.
    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. X /\ B e. X /\ ( A H B ) e. P ) ) -> ( A e. P \/ B e. P ) ) $=
      ( va vb wcel cfv wa cv co wo wi wral ccring cpridl w3a cidl biimpa simp3d
      wne ispridlc oveq1 eleq1d eleq1 orbi1d imbi12d oveq2 orbi2d rcla42v com12
      wceq exp3a 3imp2 sylan ) DUAMZCDUBNMZOZKPZLPZFQZCMZVECMZVFCMZRZSZLGTKGTZA
      GMZBGMZABFQZCMZUCACMZBCMZRZVDCDUDNMZCGUGZVMVBVCWAWBVMUCCDEFGKLHIJUHUEUFVM
      VNVOVQVTVMVNVOVQVTSZVNVOOVMWCVLWCAVFFQZCMZVRVJRZSKLABGGVEAURZVHWEVKWFWGVG
      WDCVEAVFFUIUJWGVIVRVJVEACUKULUMVFBURZWEVQWFVTWHWDVPCVFBAFUNUJWHVJVSVRVFBC
      UKUOUMUPUQUSUTVA $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc2 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
            ( A e. ( X \ P ) /\ B e. X /\ ( A H B ) e. P ) ) -> B e. P ) $=
      ( ccring wcel cpridl cfv wa cdif co w3a wn eldifn adantl wi eldifi pridlc
      3ad2ant1 ord syl3anr1 mpd ) DKLCDMNLOZAGCPLZBGLZABFQCLZRZOACLZSZBCLZUMUOU
      IUJUKUOULAGCTUEUAUJAGLZUIUKULUOUPUBAGCUCUIUQUKULROUNUPABCDEFGHIJUDUFUGUH
      $.
      $( [17-Jun-2011] $)

    $( Property of a prime ideal in a commutative ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    pridlc3 $p |- ( ( ( R e. CRing /\ P e. ( PrIdl ` R ) ) /\
          ( A e. ( X \ P ) /\ B e. ( X \ P ) ) ) -> ( A H B ) e. ( X \ P ) ) $=
      ( ccring wcel cpridl cfv wa cdif co wn eldifi wi crngorngo anim12i rngocl
      crngo 3expb syl2an adantlr ad2antll pridlc2 3exp2 imp32 con3d sylanr2 mpd
      eldifn eldif sylanbrc ) DKLZCDMNLZOZAGCPZLZBVALZOZOZABFQZGLZVFCLZRZVFVALU
      RVDVGUSURDUDLZAGLZBGLZOVGVDDUAVBVKVCVLAGCSBGCSZUBVJVKVLVGABDEFGHIJUCUEUFU
      GVEBCLZRZVIVCVOUTVBBGCUOUHVCUTVBVLVOVITVMUTVBVLOOVHVNUTVBVLVHVNTUTVBVLVHV
      NABCDEFGHIJUIUJUKULUMUNVFGCUPUQ $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d R a b $.  $d Z a b $.  $d H a b $.  $d X a b $.
    isdmn3.1 $e |- G = ( 1st ` R ) $.
    isdmn3.2 $e |- H = ( 2nd ` R ) $.
    isdmn3.3 $e |- X = ran G $.
    isdmn3.4 $e |- Z = ( Id ` G ) $.
    isdmn3.5 $e |- U = ( Id ` H ) $.
    $( The predicate "is a domain", alternate expression.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    isdmn3 $p |- ( R e. Dmn <-> ( R e. CRing /\ U =/= Z /\ A. a e. X A. b e. X
                                ( ( a H b ) = Z -> ( a = Z \/ b = Z ) ) ) ) $=
      ( wcel wa wceq wi wral cfv syl cdmn cprrng ccring wne cv co wo w3a isdmn2
      crngo csn cpridl isprrngo cidl ispridlc crngorngo biantrurd 3anass wb crn
      0idl rneqi eqtri rngo1cl eleq2 elsni syl6bir syl5com c1o cen wbr rngoueqz
      c1st rngo0cl en1eqsn eqcomd ex sylbird impbid necon3bid ovex elsn orbi12i
      elsnc imbi12i 2ralbidv anbi12d bitr3d 3bitr3d pm5.32i ancom 3bitr4i bitri
      a1i syl5bb ) AUANAUBNZAUCNZOZWQBFUDZGUEZHUEZDUFZFPZWTFPZXAFPZUGZQZHERGERZ
      UHZAUIWQWPOWQWSXHOZOWRXIWQWPXJWPAUJNZFUKZAULSNZOZWQXJACFILUMWQXMXLAUNSNZX
      LEUDZXBXLNZWTXLNZXAXLNZUGZQZHERGERZUHZXNXJXLACDEGHIJKUOWQXKXMAUPZUQYCXOXP
      YBOZOZWQXJXOXPYBURWQYEYFXJWQXOYEWQXKXOYDACFILVATUQWQXPWSYBXHWQXLEBFWQXKXL
      EPZBFPZUSYDXKYGYHXKBENZYGYHABDEECUTAVMSZUTKCYJIVBVCJMVDYGYIBXLNYHXLEBVEBF
      VFVGVHXKYHEVIVJVKZYGABCDEFIJLMKVLXKFENZYKYGQACEFIKLVNYLYKYGYLYKOEXLFEVOVP
      VQTVRVSTVTWQYAXGGHEEYAXGUSWQXQXCXTXFXBFWTXADWAWDXRXDXSXEGFWBHFWBWCWEWNWFW
      GWHWOWIWOWJWPWQWKWQWSXHURWLWM $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R a b $.  $d H a b $.  $d X a b $.  $d Z a b $.  $d A a b $.  $d B b $.
    dmnnzd.1 $e |- G = ( 1st ` R ) $.
    dmnnzd.2 $e |- H = ( 2nd ` R ) $.
    dmnnzd.3 $e |- X = ran G $.
    dmnnzd.4 $e |- Z = ( Id ` G ) $.
    $( A domain has no zero-divisors (besides zero).  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    dmnnzd $p |- ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ ( A H B ) = Z ) )
                                                  -> ( A = Z \/ B = Z ) ) $=
      ( va vb wcel co wceq wo wi cv wral cdmn wa ccring cgi cfv wne eqid isdmn3
      simp3bi oveq1 eqeq1d eqeq1 orbi1d imbi12d oveq2 orbi2d rcla42v syl5com
      exp3a 3imp2 ) CUANZAFNZBFNZABEOZGPZAGPZBGPZQZVAVBVCVEVHRZVALSZMSZEOZGPZVJ
      GPZVKGPZQZRZMFTLFTZVBVCUBVIVACUCNEUDUEZGUFVRCVSDEFGLMHIJKVSUGUHUIVQVIAVKE
      OZGPZVFVOQZRLMABFFVJAPZVMWAVPWBWCVLVTGVJAVKEUJUKWCVNVFVOVJAGULUMUNVKBPZWA
      VEWBVHWDVTVDGVKBAEUOUKWDVOVGVFVKBGULUPUNUQURUSUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    dmncan.1 $e |- G = ( 1st ` R ) $.
    dmncan.2 $e |- H = ( 2nd ` R ) $.
    dmncan.3 $e |- X = ran G $.
    dmncan.4 $e |- Z = ( Id ` G ) $.
    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan1 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ A =/= Z ) -> ( ( A H B ) = ( A H C ) -> B = C ) ) $=
      ( wcel wa co wceq sylan adantr wi 3expb cdmn w3a wne cgs cfv dmnrngo eqid
      crngo rngosubdi eqeq1d wo cgr rngogrpo syl grpodivcl adantlr dmnnzd 3exp2
      imp31 syldan exp43 3imp2 neor syl6ib com23 sylbird wb 3adant3r3 3adant3r2
      imp rngocl grpoeqdivid syl3anc 3adantr1 3imtr4d ) DUAMZAGMZBGMZCGMZUBZNZA
      HUCZNZABFOZACFOZEUDUEZOZHPZBCWFOZHPZWDWEPZBCPZWCWHAWIFOZHPZWJWCWMWGHWAWMW
      GPZWBVPDUHMZVTWODUFZABCWFDEFGIJKWFUGZUIQRUJWAWBWNWJSWAWNWBWJWAWNAHPWJUKZW
      BWJSVPVQVRVSWNWSSZVPVQVRVSWTVPVQNVRVSNZWIGMZWTVPXAXBVQVPEULMZXAXBVPWPXCWQ
      DEIUMUNZXCVRVSXBBCWFEGKWRUOTQUPVPVQXBWTVPVQXBWNWSAWIDEFGHIJKLUQURUSUTVAVB
      WJAHVCVDVEVJVFWAWKWHVGZWBWAXCWDGMZWEGMZXEVPXCVTXDRVPWPVTXFWQWPVQVRXFVSABD
      EFGIJKVKVHQVPWPVTXGWQWPVQVSXGVRACDEFGIJKVKVIQWDWEWFHEGKLWRVLVMRWAWLWJVGZW
      BVPVRVSXHVQVPXCXAXHXDXCVRVSXHBCWFHEGKLWRVLTQVNRVO $.
      $( [6-Jan-2011] $)

    $( Cancellation law for domains.  (Contributed by Jeff Madsen,
       6-Jan-2011.) $)
    dmncan2 $p |- ( ( ( R e. Dmn /\ ( A e. X /\ B e. X /\ C e. X ) )
                      /\ C =/= Z ) -> ( ( A H C ) = ( B H C ) -> A = B ) ) $=
      ( cdmn wcel w3a wa wne co wceq crngcom ccring dmncrng 3adant3r2 3adant3r1
      wb eqeq12d sylan adantr wi 3anrot biimpri dmncan1 sylanl2 sylbid ) DMNZAG
      NZBGNZCGNZOZPZCHQZPACFRZBCFRZSZCAFRZCBFRZSZABSZUTVDVGUEZVAUODUANZUSVIDUBV
      JUSPVBVEVCVFVJUPURVBVESUQACDEFGIJKTUCVJUQURVCVFSUPBCDEFGIJKTUDUFUGUHUSUOU
      RUPUQOZVAVGVHUIVKUSURUPUQUJUKCABDEFGHIJKLULUMUN $.
      $( [6-Jan-2011] $)
  $}
$( (End of Jeff Madsen's mathbox.) $)


