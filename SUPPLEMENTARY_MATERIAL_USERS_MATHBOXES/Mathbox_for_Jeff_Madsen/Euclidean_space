$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euclidean space
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The real numbers are a complete metric space.  (Contributed by Jeff
       Madsen, 2-Sep-2009.) $)
    recms $p |- ( ( abs o. - ) |` ( RR X. RR ) ) e. CMet $=
      ( vx vy cmin cr wcel cfv cc wss co cc0 3syl wceq syl syl2anc wbr cle cexp
      wb c2 adantr cabs ccom cxp cres cms copn ccld cdif cv cbl wrex wral difss
      crp cim wne eldifi imcl recn wn eldifn reim0b necon3bbid mpbid absrpcl wa
      cme eqid cnmet a1i cnmetba blssm syl3anc sseld clt cre caddc subcl syl2an
      elbl recl sqge0 resqcl addge02 absvalsq2 imsub reim0 adantl oveq2d subid1
      absresq 3eqtrd oveq1d eqtrd 3brtr4d abscl absge0 le2sq syl22anc cnmetdval
      mpbird breq2d metcl lenlt bitr3d ex con2d adantld sylbid jcad eldif ssrdv
      syl6ibr oveq2 sseq1d rcla4ev rgen isopn4 ax-mp ctop opntop ax-resscn cuni
      mpbir2an uniopn2 iscld2 mp2an mpbir wtru cncms cmsss trud ) UACUBZDDUCUDU
      EEZDYMUFFZUGFEZYPGDUHZYOEZYRYQGHZAUIZBUIZYMUJFZIZYQHZBUNUKZAYQULZGDUMUUEA
      YQYTYQEZYTUOFZUAFZUNEZYTUUIUUBIZYQHZUUEUUGUUHGEZUUHJUPZUUJUUGYTGEZUUHDEZU
      UMYTGDUQZYTURZUUHUSZKZUUGYTDEZUTUUNYTGDVAUUGUVAUUHJUUGUUOUVAUUHJLRUUQYTVB
      MVCVDUUHVENZUUGBUUKYQUUGUUAUUKEZUUAGEZUUADEZUTZVFUUAYQEUUGUVCUVDUVFUUGUUK
      GUUAUUGYMVGEZUUOUUJUUKGHUVGUUGYMYMVHZVIZVJZUUQUVBYMYTUUIGYMUVHVKZVLVMVNUU
      GUVCUVDYTUUAYMIZUUIVOOZVFZUVFUUGUVGUUOUUJUVCUVNRUVJUUQUVBUUAYMYTUUIGUVKVT
      VMUUGUVMUVFUVDUUGUVEUVMUUGUVEUVMUTZUUGUVEVFZUUIYTUUACIZUAFZPOZUVOUVPUVSUU
      ISQIZUVRSQIZPOZUVPUUHSQIZUVQVPFZSQIZUWCVQIZUVTUWAPUVPJUWEPOZUWCUWFPOZUVPU
      VQGEZUWDDEZUWGUUGUUOUVDUWIUVEUUQUUAUSZYTUUAVRVSZUVQWAZUWDWBKUVPUWCDEZUWED
      EZUWGUWHRUVPUUPUWNUUGUUPUVEUUGUUOUUPUUQUURMTZUUHWCMUVPUWIUWJUWOUWLUWMUWDW
      CKUWCUWEWDNVDUVPUUPUVTUWCLUWPUUHWKMUVPUWAUWEUVQUOFZSQIZVQIZUWFUVPUWIUWAUW
      SLUWLUVQWEMUVPUWRUWCUWEVQUVPUWQUUHSQUVPUWQUUHUUAUOFZCIZUUHJCIZUUHUUGUUOUV
      DUWQUXALUVEUUQUWKYTUUAWFVSUVPUWTJUUHCUVEUWTJLUUGUUAWGWHWIUVPUUMUXBUUHLUUG
      UUMUVEUUTTUUHWJMWLWMWIWNWOUVPUUIDEZJUUIPOZUVRDEZJUVRPOZUVSUWBRUUGUXCUVEUU
      GUUOUUPUXCUUQUURUUPUUMUXCUUSUUHWPMKTZUUGUXDUVEUUGUUMUXDUUTUUHWQMTUVPUWIUX
      EUWLUVQWPMUVPUWIUXFUWLUVQWQMUUIUVRWRWSXAUVPUUIUVLPOZUVSUVOUVPUVLUVRUUIPUU
      GUUOUVDUVLUVRLUVEUUQUWKYTUUAYMUVHWTVSXBUVPUXCUVLDEZUXHUVORUXGUVPUVGUUOUVD
      UXIUVGUVPUVIVJUUGUUOUVEUUQTUVEUVDUUGUWKWHYTUUAYMGUVKXCVMUUIUVLXDNXEVDXFXG
      XHXIXJUUAGDXKXMXLUUDUULBUUIUNUUAUUILUUCUUKYQUUAUUIYTUUBXNXOXPNXQUVGYRYSUU
      FVFRUVIABYQYMYOGUVKYOVHZXRXSYDYOXTEZDGHZYPYRRUVGUXKUVIYMYOUXJYAXSYBDYOGUV
      GGYOYCLUVIYMYOGUVKUXJYEXSYFYGYHYNYPRYIYMYOGDUVKUXJYMUEEYIYMUVHYJVJUXLYIYB
      VJYKYLYH $.
      $( [2-Sep-2009] $)
  $}

  $c Rn $.
  $( Extend class notation with the n-dimensional Euclidean space. $)
  crrn $a class Rn $.

  ${
    $d n r x y d k $.
    $( Define n-dimensional Euclidean space as a metric space with the standard
       Euclidean norm given by the quadratic mean. $)
    df-rrn $a |- Rn = ( n e. NN |->
       ( x e. ( RR ^m ( 1 ... n ) ) , y e. ( RR ^m ( 1 ... n ) ) |-> ( sqr `
         sum_ k e. ( 1 ... n ) ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) ) $.
  $}

  ${
    $d N n r x y d k $.
    $( The n-dimensional Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnval $p |- ( N e. NN -> ( Rn ` N ) =
        ( x e. ( RR ^m ( 1 ... N ) ) , y e. ( RR ^m ( 1 ... N ) ) |-> ( sqr `
          sum_ k e. ( 1 ... N ) ( ( ( x ` k ) - ( y ` k ) ) ^ 2 ) ) ) ) $=
      ( vn cr c1 cv cfz co cmap cfv cmin c2 cexp csu csqr cmpt2 cn crrn sumeq1d
      wceq oveq2 oveq2d fveq2d mpt2eq123dv df-rrn ovex mpt2ex fvmpt ) EDABFGEHZ
      IJZKJZUMULCHZAHLUNBHLMJNOJZCPZQLZRABFGDIJZKJZUSURUOCPZQLZRSTUKDUBZABUMUMU
      QUSUSVAVBULURFKUKDGIUCZUDZVDVBUPUTQVBULURUOCVCUAUEUFABCEUGABUSUSVAFURKUHZ
      VEUIUJ $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.  $d X x y d k $.  $d Y x y d k $.
    $( The value of the Euclidean metric.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnmval $p |- ( ( N e. NN /\ X e. ( RR ^m ( 1 ... N ) )
                  /\ Y e. ( RR ^m ( 1 ... N ) ) ) -> ( X ( Rn ` N ) Y ) =
        ( sqr ` sum_ k e. ( 1 ... N ) ( ( ( X ` k ) - ( Y ` k ) ) ^ 2 ) ) ) $=
      ( vx vy cn wcel cr co cfv cv cmin c2 cexp csu csqr wceq wa fveq1 cfz cmap
      c1 crrn cmpt2 rrnval oveqd oveqan12d oveq1d sumeq2sdv fveq2d eqid ovmpt2a
      fvex sylan9eq 3impb ) BGHZCIUCBUAJZUBJZHZDUSHZCDBUDKZJZURALZCKZVDDKZMJZNO
      JZAPZQKZRUQUTVASVCCDEFUSUSURVDELZKZVDFLZKZMJZNOJZAPZQKZUEZJVJUQVBVSCDEFAB
      UFUGEFCDUSUSVRVJVSVKCRZVMDRZSZVQVIQWBURVPVHAWBVOVGNOVTWAVLVEVNVFMVDVKCTVD
      VMDTUHUIUJUKVSULVIQUNUMUOUP $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k $.
    $( The underlying set of Euclidean space.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrndm $p |- ( N e. NN -> dom dom ( Rn ` N ) = ( RR ^m ( 1 ... N ) ) ) $=
      ( vx vy vk cn wcel crrn cfv cdm cr c1 cfz co cmap cxp cv cmin csqr syl6eq
      dmeqd c2 cexp csu cmpt2 rrnval eqid fvex dmmpt2 dmxpid ) AEFZAGHZIZIJKALM
      ZNMZUNOZIUNUJULUOUJULBCUNUNUMDPZBPHUPCPHQMUAUBMDUCZRHZUDZIUOUJUKUSBCDAUET
      BCUNUNURUSUSUFUQRUGUHSTUNUIS $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d N x y d k z m n $.
    $( Euclidean space is a metric space.  (Contributed by Jeff Madsen,
       2-Sep-2009.)  (The proof was shortened by Mario Carneiro,
       5-Jun-2014.) $)
    rrnmet $p |- ( N e. NN -> ( Rn ` N ) e. Met ) $=
      ( vx vy vk wcel cfv cr wf co cc0 wceq wb caddc cle wral cexp csqr syl2anc
      wa c2 vz cn crrn cdm cxp cv wbr cme c1 cfz cmap cmin csu cmpt2 fzfid reex
      simprl elmap sylib ffvelrn sylan simprr resubcl resqcl syl fsumrecl sqge0
      ovex fsumge0 resqrcl ralrimivva fmpt2 rrnval xpeq12d feq12d mpbird eleq2d
      eqid rrndm anbi12d biimpa sqr00 fsum00 cc recnd sqeq0 subeq0 bitrd 3bitrd
      ralbidva rrnmval 3expb eqeq1d wfn ffn eqfnfv2 3bitr4d adantlr simpr trirn
      adantr npncan syl3anc oveq1d sumeq2dv sqsubswap 3brtr3d 3adant3r 3adant3l
      fveq2d w3a oveq12d 3expa an32s 3brtr4d ralrimiva jca cvv fvex ismet ax-mp
      syldan sylanbrc ) AUBEZAUCFZUDUDZYFUEZGYEHZBUFZCUFZYEIZJKZYIYJKZLZYKUAUFZ
      YIYEIZYOYJYEIZMIZNUGZUAYFOZSZCYFOBYFOZYEUHEZYDYHGUIAUJIZUKIZUUEUEZGBCUUEU
      UEUUDDUFZYIFZUUGYJFZULIZTPIZDUMZQFZUNZHZYDUUMGEZCUUEOBUUEOUUOYDUUPBCUUEUU
      EYDYIUUEEZYJUUEEZSZSZUULGEZJUULNUGZUUPUUTUUDUUKDUUTUIAUOZUUTUUGUUDEZSZUUJ
      GEZUUKGEUVEUUHGEZUUIGEZUVFUUTUUDGYIHZUVDUVGUUTUUQUVIYDUUQUURUQGUUDYIUPUIA
      UJVHZURUSZUUDGUUGYIUTVAZUUTUUDGYJHZUVDUVHUUTUURUVMYDUUQUURVBGUUDYJUPUVJUR
      USZUUDGUUGYJUTVAZUUHUUIVCRZUUJVDVEZVFZUUTUUDUUKDUVCUVQUVEUVFJUUKNUGUVPUUJ
      VGVEZVIZUULVJRVKBCUUEUUEUUMGUUNUUNVRVLUSYDYGUUFGYEUUNBCDAVMYDYFUUEYFUUEAV
      SZUWAVNVOVPYDUUABCYFYFYDYIYFEZYJYFEZSZUUSUUAYDUWDUUSYDUWBUUQUWCUURYDYFUUE
      YIUWAVQYDYFUUEYJUWAVQVTWAUUTYNYTUUTUUMJKZUUHUUIKZDUUDOZYLYMUUTUWEUULJKZUU
      KJKZDUUDOUWGUUTUVAUVBUWEUWHLUVRUVTUULWBRUUTUUDUUKDUVCUVQUVSWCUUTUWIUWFDUU
      DUVEUWIUUJJKZUWFUVEUUJWDEUWIUWJLUVEUUJUVPWEUUJWFVEUVEUUHWDEZUUIWDEZUWJUWF
      LUVEUUHUVLWEZUVEUUIUVOWEZUUHUUIWGRWHWJWIUUTYKUUMJYDUUQUURYKUUMKZDAYIYJWKW
      LZWMUUTYIUUDWNZYJUUDWNZYMUWGLUUTUVIUWQUVKUUDGYIWOVEUUTUVMUWRUVNUUDGYJWOVE
      DUUDYIYJWPRWQUUTYSUAYFUUTYOYFEZYOUUEEZYSUUTUWSUWTYDUWSUWTLUUSYDYFUUEYOUWA
      VQXAWAUUTUWTSZUUMUUDUUGYOFZUUHULITPIZDUMZQFZUUDUXBUUIULIZTPIDUMQFZMIZYKYR
      NUXAUUDUUHUXBULIZUXFMIZTPIZDUMZQFUUDUXITPIZDUMZQFZUXGMIUUMUXHNUXAUUDUXIUX
      FDUXAUIAUOUXAUVDSZUVGUXBGEZUXIGEUUTUVDUVGUWTUVLWRUXAUUDGYOHZUVDUXQUXAUWTU
      XRUUTUWTWSGUUDYOUPUVJURUSUUDGUUGYOUTVAZUUHUXBVCRUXPUXQUVHUXFGEUXSUUTUVDUV
      HUWTUVOWRUXBUUIVCRWTUXAUXLUULQUXAUUDUXKUUKDUXPUXJUUJTPUXPUWKUXBWDEZUWLUXJ
      UUJKUUTUVDUWKUWTUWMWRZUXPUXBUXSWEZUUTUVDUWLUWTUWNWRUUHUXBUUIXBXCXDXEXJUXA
      UXOUXEUXGMUXAUXNUXDQUXAUUDUXMUXCDUXPUWKUXTUXMUXCKUYAUYBUUHUXBXFRXEXJXDXGU
      UTUWOUWTUWPXAYDUWTUUSYRUXHKZYDUWTUUSUYCYDUWTUUSXKYPUXEYQUXGMYDUWTUUQYPUXE
      KUURDAYOYIWKXHYDUWTUURYQUXGKUUQDAYOYJWKXIXLXMXNXOYBXPXQYBVKYEXREUUCYHUUBS
      LAUCXSBCUAXRYEYFYFVRXTYAYC $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d k A $.  $d j k m n t x F $.  $d x J $.  $d j k n M $.  $d j k m n x N $.
    $d k n G $.  $d j k n x P $.  $d j k n x ph $.  $d k n R $.  $d k n X $.
    rrndstprj1.1 $e |- M = ( ( abs o. - ) |` ( RR X. RR ) ) $.
    rrndstprj1.2 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    $( The distance between two points in Euclidean space is greater than the
       distance between the projections onto one coordinate.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj1 $p |- ( ( ( N e. NN /\ A e. ( 1 ... N ) ) /\ ( F e. X /\ G e. X
                    ) ) -> ( ( F ` A ) M ( G ` A ) ) <_ ( F ( Rn ` N ) G ) ) $=
      ( vk wcel co wa cfv c2 cexp cle wbr cr syl2anc syl cn c1 cfz cmin cabs cv
      csu csqr crrn fzfid wf cmap simprl syl6eleq reex ovex elmap sylib ffvelrn
      sylan simprr resubcl resqcl cc0 sqge0 fveq2 oveq12d oveq1d simplr fsumge1
      absresq fsumrecl fsumge0 resqrth 3brtr4d wb cc recnd abscl absge0 resqrcl
      wceq sqrge0 le2sq syl22anc mpbird remetdval anbi12i rrnmval 3expb sylan2b
      eleq2i adantlr ) EUAJZAUBEUCKZJZLZBFJZCFJZLZLZABMZACMZUDKZUEMZWOIUFZBMZXF
      CMZUDKZNOKZIUGZUHMZXBXCDKZBCEUIMKZPXAXEXLPQZXENOKZXLNOKZPQZXAXDNOKZXKXPXQ
      PXAWOXJXSIAXAUBEUJZXAXFWOJZLZXIRJZXJRJYBXGRJZXHRJZYCXAWORBUKZYAYDXABRWOUL
      KZJZYFXABFYGWQWRWSUMHUNRWOBUOUBEUCUPZUQURZWORXFBUSUTXAWORCUKZYAYEXACYGJZY
      KXACFYGWQWRWSVAHUNRWOCUOYIUQURZWORXFCUSUTXGXHVBSZXIVCTZYBYCVDXJPQYNXIVETZ
      XFAWBZXIXDNOYQXGXBXHXCUDXFABVFXFACVFVGVHWNWPWTVIZVJXAXDRJZXPXSWBXAXBRJZXC
      RJZYSXAYFWPYTYJYRWORABUSSZXAYKWPUUAYMYRWORACUSSZXBXCVBSZXDVKTXAXKRJZVDXKP
      QZXQXKWBXAWOXJIXTYOVLZXAWOXJIXTYOYPVMZXKVNSVOXAXERJZVDXEPQZXLRJZVDXLPQZXO
      XRVPXAXDVQJZUUIXAXDUUDVRZXDVSTXAUUMUUJUUNXDVTTXAUUEUUFUUKUUGUUHXKWASXAUUE
      UUFUULUUGUUHXKWCSXEXLWDWEWFXAYTUUAXMXEWBUUBUUCXBXCDGWGSWNWTXNXLWBZWPWTWNY
      HYLLUUOWRYHWSYLFYGBHWLFYGCHWLWHWNYHYLUUOIEBCWIWJWKWMVO $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)

    $( Bound on the distance between two points in Euclidean space given bounds
       on the distances in each coordinate.  This theorem and ~ rrndstprj1 can
       be used to show that the supremum norm and Euclidean norm are
       equivalent.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrndstprj2 $p |- ( ( ( N e. NN /\ F e. X /\ G e. X ) /\ ( R e. RR+ /\
                    A. n e. ( 1 ... N ) ( ( F ` n ) M ( G ` n ) ) < R ) ) ->
                                ( F ( Rn ` N ) G ) < ( R x. ( sqr ` N ) ) ) $=
      ( vk wcel cfv co clt wbr c1 cr wceq syl syl2anc cn w3a crp cv cfz wral wa
      crrn cmin c2 cexp csu csqr cmul cmap simpl1 simpl2 simpl3 rrnmval syl3anc
      syl6eleq fzfid c0 wne cuz nnuz eluzfz1 ne0i reex ovex elmap sylib ffvelrn
      wf sylan resubcl resqcl simprl rpre adantr absresq remetdval simprr fveq2
      cabs oveq12d breq1d rcla4cva eqbrtrrd cc0 cle wb recnd abscl absge0 rpge0
      cc lt2sq syl22anc mpbid fsumlt fsumrecl sqge0 fsumge0 resqrth nnrp mulcom
      chash oveq2d cn0 nnnn0 hashfz1 oveq1d 3eqtr4d rpsqrcl sqmul cfn fsumconst
      rpcn 3brtr4d resqrcl sqrge0 rpmulcl mpbird eqbrtrd ) FUAKZCGKZDGKZUBZAUCK
      ZBUDZCLZYKDLZEMZANOZBPFUEMZUFZUGZUGZCDFUHLMZYPJUDZCLZUUADLZUIMZUJUKMZJULZ
      UMLZAFUMLZUNMZNYSYFCQYPUOMZKZDUUJKZYTUUGRYFYGYHYRUPZYSCGUUJYFYGYHYRUQIVAZ
      YSDGUUJYFYGYHYRURIVAZJFCDUSUTYSUUGUUINOZUUGUJUKMZUUIUJUKMZNOZYSUUFYPAUJUK
      MZJULZUUQUURNYSYPUUEUUTJYSPFVBZYSPYPKZYPVCVDYSFPVELZKUVCYSFUAUVDUUMVFVAPF
      VGSYPPVHSYSUUAYPKZUGZUUDQKZUUEQKUVFUUBQKZUUCQKZUVGYSYPQCVNZUVEUVHYSUUKUVJ
      UUNQYPCVIPFUEVJZVKVLYPQUUACVMVOZYSYPQDVNZUVEUVIYSUULUVMUUOQYPDVIUVKVKVLYP
      QUUADVMVOZUUBUUCVPTZUUDVQSZYSUUTQKZUVEYSAQKZUVQYSYJUVRYIYJYQVRZAVSSZAVQSZ
      VTUVFUUDWELZUJUKMZUUEUUTNUVFUVGUWCUUERUVOUUDWASUVFUWBANOZUWCUUTNOZUVFUUBU
      UCEMZUWBANUVFUVHUVIUWFUWBRUVLUVNUUBUUCEHWBTYSYQUVEUWFANOZYIYJYQWCYOUWGBUU
      AYPYKUUARZYNUWFANUWHYLUUBYMUUCEYKUUACWDYKUUADWDWFWGWHVOWIUVFUWBQKZWJUWBWK
      OZUVRWJAWKOZUWDUWEWLUVFUUDWQKZUWIUVFUUDUVOWMZUUDWNSUVFUWLUWJUWMUUDWOSYSUV
      RUVEUVTVTYSUWKUVEYSYJUWKUVSAWPSVTUWBAWRWSWTWIXAYSUUFQKZWJUUFWKOZUUQUUFRYS
      YPUUEJUVBUVPXBZYSYPUUEJUVBUVPUVFUVGWJUUEWKOUVOUUDXCSXDZUUFXETYSUUTUUHUJUK
      MZUNMZYPXHLZUUTUNMZUURUVAYSUUTFUNMZFUUTUNMZUWSUXAYSUUTWQKZFWQKUXBUXCRYSUU
      TUWAWMZYSFYSFUCKZFQKZYSYFUXFUUMFXFSZFVSSZWMUUTFXGTYSUWRFUUTUNYSUXGWJFWKOZ
      UWRFRUXIYSUXFUXJUXHFWPSFXETXIYSUWTFUUTUNYSFXJKZUWTFRYSYFUXKUUMFXKSFXLSXMX
      NYSAWQKUUHWQKZUURUWSRYSAUVTWMYSUUHUCKZUXLYSUXFUXMUXHFXOSZUUHXSSAUUHXPTYSY
      PXQKUXDUVAUXARUVBUXEYPUUTJXRTXNXTYSUUGQKZWJUUGWKOZUUIQKZWJUUIWKOZUUPUUSWL
      YSUWNUWOUXOUWPUWQUUFYATYSUWNUWOUXPUWPUWQUUFYBTYSUUIUCKZUXQYSYJUXMUXSUVSUX
      NAUUHYCTZUUIVSSYSUXSUXRUXTUUIWPSUUGUUIWRWSYDYE $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)

    rrncms.3 $e |- J = ( MetOpen ` ( Rn ` N ) ) $.
    rrncms.4 $e |- ( ph -> N e. NN ) $.
    rrncms.5 $e |- ( ph -> F e. ( Cau ` ( Rn ` N ) ) ) $.
    rrncms.6 $e |- ( ph -> F : NN --> X ) $.
    rrncms.7 $e |- P = ( m e. ( 1 ... N ) |->
                         ( ~~> ` ( t e. NN |-> ( ( F ` t ) ` m ) ) ) ) $.
    $( Lemma for ~ rrncms . $)
    rrncmslem $p |- ( ph -> F e. dom ( ~~>t ` J ) ) $=
      ( vk vj wcel cn vx vn clm cfv wrel wbr cdm lmrel crrn cv co clt wral wrex
      cuz crp cr c1 cfz cmap wf wfn cmpt cli cvv fnmpt fvex a1i mprg wa nnuz cz
      1z wceq fveq2 fveq1d eqid fvmpt adantl ffvelrn sylan syl6eleq elmap sylib
      reex ovex an32s eqeltrd recnd cmin cabs cca cme rrnmet syl eqidd wb rrndm
      syl6eqr feq3 mpbird iscauf mpbid adantr ad3antrrr simpllr adantll syl2anc
      cle uztrn2 simplr rrndstprj1 syl22anc eleqtrrd metsym syl3anc adantllr wi
      breqtrd metcl ad2antrr ralimdva reximdva remetdval ad2antlr fveq2d breq1d
      ralbidva rexbidva sylibd mpd ralrimiva sylibr adantlr rexuz3 ax-mp sylan2
      anassrs cc rpcn remet simpll remetba rpre lelttr mpand oveq12d nnex mptex
      eqtr4d ralbidv caucvg mpteq2dv breqtrrd climrecl ffnfv sylanbrc csqr cdiv
      climdm id nnrp rpsqrcl 3syl rpdivcl syl2anr climi2 syl5bbr cfn fzfi bitri
      rexfiuz cmul eleqtrd w3a rrndstprj2 syl31anc cc0 wne rpne0 divcan1 breq2d
      expr lmmbrf mpbir2and releldm sylancr ) AFUCUDZUEECUWHUFZEUWHUGSFUHAUWICH
      UIUDZUGUGZSQUJZEUDZCUWJUKZUAUJZULUFZQRUJZUOUDZUMZRTUNZUAUPUMACUQURHUSUKZU
      TUKZUWKAUXAUQCVAZCUXBSACUXAVBZUBUJZCUDZUQSZUBUXAUMUXCUXDABTDUJZBUJZEUDZUD
      ZVCZVDUDZVESZUXDDUXADUXAUXMCVEPVFUXNUXHUXASUXLVDVGVHVIVHAUXGUBUXAAUXEUXAS
      ZVJZUXFQBTUXEUXJUDZVCZURVETVKURVLSZUXPVMVHUXPUXRUXRVDUDZUXFVDUXPUXRVDUGSU
      XRUXTVDUFUXPUARQUXRURVETVKUXPUWLTSZVJZUWLUXRUDZUYBUYCUXEUWMUDZUQUYAUYCUYD
      VNZUXPBUWLUXQUYDTUXRUXIUWLVNUXEUXJUWMUXIUWLEVOVPUXRVQZUXEUWMVGVRZVSAUYAUX
      OUYDUQSZAUYAVJZUXAUQUWMVAZUXOUYHUYIUWMUXBSUYJUYIUWMIUXBATIEVAZUYAUWMISZOT
      IUWLEVTZWAKWBUQUXAUWMWEURHUSWFZWCWDUXAUQUXEUWMVTWAWGZWHZWIUXPUWQEUDZUWMUW
      JUKZUWOULUFZQUWRUMZRTUNZUAUPUMZUYCUWQUXRUDZWJUKZWKUDZUWOULUFZQUWRUMZRTUNZ
      UAUPUMZAVUBUXOAEUWJWLUDSVUBNAUAUWMUYQUWJRQEURUWKTUWKVQZVKAHTSZUWJWMSZMHWN
      WOZUXSAVMVHZUYIUWMWPZAUWQTSZVJZUYQWPATUWKEVAZUYKOAUWKIVNZVURUYKWQAUWKUXBI
      AVUKUWKUXBVNMHWRWOZKWSZUWKITEWTWOXAZXBXCXDUXPVUBUYDUXEUYQUDZGUKZUWOULUFZQ
      UWRUMZRTUNZUAUPUMVUIUXPVUAVVGUAUPUXPUWOUPSZVJZUYTVVFRTVVIVUPVJZUYSVVEQUWR
      VVJUWLUWRSZVJZVVDUYRXIUFZUYSVVEUXPVUPVVKVVMVVHUXPVUPVJZVVKVJZVVDUWMUYQUWJ
      UKZUYRXIVVOVUKUXOUYLUYQISZVVDVVPXIUFAVUKUXOVUPVVKMXEAUXOVUPVVKXFVVOUYKUYA
      UYLAUYKUXOVUPVVKOXEZVUPVVKUYAUXPURUWLUWQTVKXJZXGZUYMXHZVVOUYKVUPVVQVVRUXP
      VUPVVKXKTIUWQEVTZXHZUXEUWMUYQGHIJKXLXMVVOVULUWMUWKSZUYQUWKSZVVPUYRVNAVULU
      XOVUPVVKVUMXEZVVOUWMIUWKVWAAVUSUXOVUPVVKVVAXEZXNZVVOUYQIUWKVWCVWGXNZUWMUY
      QUWJUWKVUJXOXPXSXQVVLVVDUQSZUYRUQSZUWOUQSZVVMUYSVJVVEXRUXPVUPVVKVWJVVHVVO
      GWMSZUYHVVCUQSZVWJVWMVVOGJUUAVHVVOUXPUYAUYHUXPVUPVVKUUBVVTUYOXHZVVNVWNVVK
      AVUPUXOVWNVUQUXAUQUYQVAZUXOVWNVUQUYQUXBSVWPVUQUYQIUXBAUYKVUPVVQOVWBWAKWBU
      QUXAUYQWEUYNWCWDUXAUQUXEUYQVTWAWGXDZUYDVVCGUQGJUUCXTXPXQUXPVUPVVKVWKVVHVV
      OVULVWEVWDVWKVWFVWIVWHUYQUWMUWJUWKVUJXTXPXQVVIVWLVUPVVKVVHVWLUXPUWOUUDVSY
      AVVDUYRUWOUUEXPUUFYBYCYBUXPVVGVUHUAUPUXPVVFVUGRTVVNVVEVUFQUWRVVOVVDVUEUWO
      ULVVOVVDUYDVVCWJUKZWKUDZVUEVVOUYHVWNVVDVWSVNVWOVWQUYDVVCGJYDXHVVOVUDVWRWK
      VVOUYCUYDVUCVVCWJVVOUYAUYEVVTUYGWOVUPVUCVVCVNUXPVVKBUWQUXQVVCTUXRUXIUWQVN
      UXEUXJUYQUXIUWQEVOVPUYFUXEUYQVGVRYEUUGYFUUJYGYHYIUUKYJYKUXRVESUXPBTUXQUUH
      UUIVHUULUXRUUTWDUXOUXFUXTVNADUXEUXMUXTUXACUXHUXEVNZUXLUXRVDVWTBTUXKUXQUXH
      UXEUXJVOUUMYFPUXRVDVGVRVSUUNZUYPUXFVESZUXPUXECVGZVHUUOZYLUBUXAUQCUUPUUQUQ
      UXACWEUYNWCYMVUTXNZAUWTUAUPAVVHVJZUYDUXFGUKZUWOHUURUDZUUSUKZULUFZUBUXAUMZ
      QUWRUMZRTUNZUWTVXFVXJQUWRUMZRVLUNZUBUXAUMZVXMVXFVXOUBUXAVXFUXOVJZVXOUYDUX
      FWJUKWKUDZVXIULUFZQUWRUMZRTUNZVXQUXFUYDVXIRQUXRURVETVKUXSVXQVMVHVXFVXIUPS
      ZUXOVVHVVHVXHUPSZVYBAVVHUVAAVUKHUPSVYCMHUVBHUVCUVDZUWOVXHUVEUVFZXDUYAUYEV
      XQUYGVSAUXOUXRUXFVDUFVVHVXAYNVXBVXQVXCVHUVGVXOVXNRTUNZVXQVYAUXSVYFVXOWQVM
      VXJRQURTVKYOYPVXQVXNVXTRTVXQVUPVJVXJVXSQUWRVXQVUPVVKVXJVXSWQZVUPVVKVJZVXQ
      UYAVYGVVSVXQUYAVJZVXGVXRVXIULVYIUYHUXGVXGVXRVNAUXOUYAUYHVVHUYOXQVXQUXGUYA
      AUXOUXGVVHVXDYNXDUYDUXFGJYDXHYGYQYRYHYIUVHXAYLVXMVXLRVLUNZVXPUXSVXMVYJWQV
      MVXKRQURTVKYOYPUXAUVISVYJVXPWQURHUVJVXJUXARQUBUVLYPUVKYMVXFVXLUWSRTVXFVUP
      VJVXKUWPQUWRVXFVUPVVKVXKUWPXRZVYHVXFUYAVYKVVSVXFUYAVJZVXKUWNVXIVXHUVMUKZU
      LUFZUWPVYLVUKUYLCISZVYBVXKVYNXRAVUKVVHUYAMYAVXFUYKUYAUYLAUYKVVHOXDUYMWAAV
      YOVVHUYAACUWKIVXEVVAUVNYAVXFVYBUYAVYEXDVUKUYLVYOUVOVYBVXKVYNVXIUBUWMCGHIJ
      KUVPUWCUVQVYLVYMUWOUWNULVYLUWOYSSZVXHYSSZVXHUVRUVSZVYMUWOVNVVHVYPAUYAUWOY
      TYEVYLVYCVYQAVYCVVHUYAVYDYAZVXHYTWOVYLVYCVYRVYSVXHUVTWOUWOVXHUWAXPUWBYJYQ
      YRYBYCYKYLAUAUWMUWJCRQEFURUWKUWKTVUJLVUMVXEVKVUNVUOVVBUWDUWEECUWHUWFUWG
      $.
      $( [6-Jun-2014] $)
  $}

  ${
    $d f j k m n t x N $.
    $( Euclidean space is complete.  (Contributed by Jeff Madsen, 2-Sep-2009.)
       (Revised by Mario Carneiro, 6-Jun-2014.) $)
    rrncms $p |- ( N e. NN -> ( Rn ` N ) e. CMet ) $=
      ( vf vt vm cn wcel crrn cfv cms cdm cv wf copn clm wa c1 co cmpt cr eqid
      wi cca wral cfz cli cabs cmin ccom cxp cres cmap simpll simplr wceq rrndm
      wb adantr feq3 syl biimpa rrncmslem ex ralrimiva nnuz cz 1z rrnmet iscms3
      a1i mpbird ) AEFZAGHZIFEVLJJZBKZLZVNVLMHZNHJFZUAZBVLUBHZUCVKVRBVSVKVNVSFZ
      OZVOVQWAVOOCDPAUDQZCEDKCKVNHHRUEHRZDVNVPUFUGUHSSUIUJZASWBUKQZWDTWETVPTZVK
      VTVOULVKVTVOUMWAVOEWEVNLZWAVMWEUNZVOWGUPVKWHVTAUOUQVMWEEVNURUSUTWCTVAVBVC
      VKVLBVPPVMEVDVMTWFPVEFVKVFVIAVGVHVJ $.
      $( [6-Jun-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d X a b d f m n r u v w x y z $.  $d M a b d f m n r u v w x y z $.
    $d Y a b d f m n r u v w x y z $.  $d N a b d f m n r u v w x y z $.
    rrntotbnd.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrntotbnd.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem1 $p |- ( ( ( ( N e. NN /\ x e. X ) /\ ( d e. RR+ /\ r e. RR+ )
          ) /\ ( y e. dom dom ( Rn ` N ) /\ ( x ( Rn ` N ) y ) < r ) ) ->
        ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
            ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\
            v = ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( ball ` ( Rn ` N ) ) d ) e.
        { a | E. f ( f : ( 1 ... N ) --> ( -u ( ( |_ ` ( r / ( d / ( sqr ` N )
            ) ) ) + 1 ) ... ( ( |_ ` ( r / ( d / ( sqr ` N ) ) ) ) + 1 ) ) /\
                a = ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
                                ( ( d / ( sqr ` N ) ) x. ( f ` m ) ) ) ) }
                                            ( ball ` ( Rn ` N ) ) d ) ) } ) $=
      ( wcel wa co cr cn cv crp crrn cfv cdm clt wbr c1 cfz csqr cdiv cfl caddc
      cneg wf cmin c2 wceq copab cmul cbl wex cab wral cabs cle cn0 simplr nnrp
      rpsqrcl syl rpdivcl sylan2 ancoms adantlr syl2anc cc0 rpre rpge0 flge0nn0
      nn0p1nn 3syl an31s ad2antrr wne cmap rrndm eleq2d reex ovex syl6bb biimpa
      elmap ffvelrn sylan adantllr eleq2i bitri sylanb adantll resubcl ad2ant2r
      rpne0 redivcl syl3anc 2re 2ne0 rereccli readdcl sylancl flcl adantlrr zcn
      cz abscl recnd peano2zdi zre anasss peano2re absrdbnd absdiv absid oveq2d
      cc eqtrd recn cme rrnmet adantr a1i wi eqbrtrd letrd sylib mpbid 3ad2ant1
      eqid wb syl6reqr simpr metcl ad2antll ccom remetdval remet remetba metsym
      cxp cres biimprd imdistani rrndstprj1 an32s anassrs imp eqbrtrrd ad2antlr
      ex adantl ltle impr elrp lediv1 flwordi 1re leadd1 w3a nnre absle syl2anr
      biimp3a simp2 nnnegz nnz elfz mpbird ralrimiva fopab2 eqidd opabex2 fveq1
      feq1 fnopabeqd oveq1d eqeq2d anbi12d cla4ev eqeq1 anbi2d exbidv sylibr
      elab ) IUAQZAUBZJQZRZNUBZUCQZLUBZUCQZRZRZBUBZIUDUEZUFUFZQZUWPUXEUXFSZUXAU
      GUHZRZRZUIIUJSZUXAUWSIUKUEZULSZULSZUMUEZUIUNSZUOZUXRUJSZEUBZUPZFUBZUXMQZD
      UBZUYCUWPUEZUXOUYCGUBZUXMQZCUBUYGUXEUEZUYGUWPUEZUQSZUXOULSZUIURULSZUNSZUM
      UEZUSRGCUTZUEZVASZUNSZUSRFDUTZUWSUXFVBUEZSZUYDUYEUYFUXOUYCUYAUEZVASZUNSZU
      SRFDUTZUWSVUASZUSZRZEVCZVUBUYBMUBZVUGUSZRZEVCZMVDQUXLUXMUXTUYPUPZVUBVUBUS
      ZVUJUXLUYOUXTQZGUXMVEVUOUXLVUQGUXMUXLUYHRZUXRUAQZUYOXOQZUYOVFUEZUXRVGUHZV
      UQUXDVUSUXKUYHUWOUXCVUSUWQUXCUWOVUSUWOUXBUWTVUSUWOUXBRUWTRZUXPUCQZUXQVHQZ
      VUSVVCUXBUXOUCQZVVDUWOUXBUWTVIUWOUWTVVFUXBUWTUWOVVFUWOUWTUXNUCQZVVFUWOIUC
      QVVGIVJIVKVLUWSUXNVMVNZVOZVPUXAUXOVMZVQVVDUXPTQZVRUXPVGUHVVEUXPVSZUXPVTUX
      PWAVQUXQWBWCWDVOVPWEUXDUXHUYHVUTUXJUXDUXHRZUYHRZUYNTQZVUTVVNUYLTQZUYMTQVV
      OVVNUYKTQZUXOTQZUXOVRWFZVVPUWRUXHUYHVVQUXCUWRUXHRZUYHRZUYITQZUYJTQZVVQUWO
      UXHUYHVWBUWQUWOUXHRUXMTUXEUPZUYHVWBUWOUXHVWDUWOUXHUXETUXMWGSZQVWDUWOUXGVW
      EUXEIWHZWITUXMUXEWJUIIUJWKZWNWLWMUXMTUYGUXEWOWPWQZUWRUYHVWCUXHUWQUYHVWCUW
      OUWQUXMTUWPUPZUYHVWCUWQUWPVWEQVWIJVWEUWPOWRTUXMUWPWJVWGWNWSUXMTUYGUWPWOWT
      XAVPZUYIUYJXBVQWQZUXDVVRUXHUYHUWOUWTVVRUWQUXBUWTUWOVVRUWTUWORZVVFVVRVVHUX
      OVSVLVOZXCWEUXDVVSUXHUYHUWOUWTVVSUWQUXBUWTUWOVVSVWLVVFVVSVVHUXOXDVLVOXCZW
      EUYKUXOXEXFZURXGXHXIUYLUYMXJXKZUYNXLZVLXMVURVVAUYLVFUEZUMUEZUIUNSZUXRVURU
      YOYFQZVVATQVURVVOVUTVXAUXDUXHUYHVVOUXJVWPXMVWQUYOXNWCUYOXPVLVURVWRTQZVWTX
      OQVWTTQUXDUXHUYHVXBUXJVVNUYLYFQVXBVVNUYLVWOXQUYLXPVLXMZVXBVWSVWRXLZXRVWTX
      SWCVURUXQTQZUXRTQZUXDVXEUXKUYHUWOUXCVXEUWQUWOUXCRVVKUXQXOQVXEUWOUWTUXBVVK
      UXBUWOUWTRZVVKUXBVXGRVVDVVKVXGUXBVVFVVDVVIVVJVNVVLVLVOXTZUXPXLUXQXSWCVPWE
      ZUXQYAVLVURVVPVVAVWTVGUHUXDUXHUYHVVPUXJVWOXMUYLYBVLVURVWSUXQVGUHZVWTUXRVG
      UHZVURVXBVVKVWRUXPVGUHVXJVXCUXDVVKUXKUYHUWOUXCVVKUWQVXHVPWEVURVWRUYKVFUEZ
      UXOULSZUXPVGVURVWRVXLUXOVFUEZULSZVXMVURUYKYFQZUXOYFQZVVSVWRVXOUSVURUYKUXD
      UXHUYHVVQUXJVWKXMZXQUXDVXQUXKUYHUWOUWTVXQUWQUXBVXGUXOVWMXQXCWEUXDVVSUXKUY
      HVWNWEUYKUXOYCXFVURVXNUXOVXLULUXDVXNUXOUSZUXKUYHUWOUWTVXSUWQUXBVXGVVRVRUX
      OVGUHZVXSVWMVXGVVFVXTVVIUXOVTVLUXOYDVQXCWEYEYGVURVXLUXAVGUHZVXMUXPVGUHZVU
      RVXLUXIUXAVURVVQVXPVXLTQZVXRUYKYHUYKXPWCZUXLUXITQZUYHUWRUXHVYEUXCUXJVVTUX
      FYIQZUWPUXGQZUXHVYEUWOVYFUWQUXHIYJWEUWRVYGUXHUWOUWQVYGUWOJUXGUWPUWOUXGVWE
      JVWFOUUAZWIWMYKUWRUXHUUBUWPUXEUXFUXGUXGYSUUCXFZXCYKUXDUXATQZUXKUYHUXBVYJU
      WRUWTUXAVSZUUDWEZUWRUXKUYHVXLUXIVGUHZUXCUWRUXHUYHVYMUXJVWAUYIUYJVFUQUUETT
      UUJUUKZSZVXLUXIVGVWAVWBVWCVYOVXLUSVWHVWJUYIUYJVYNVYNYSZUUFVQVWAVYOUYJUYIV
      YNSZUXIVGVWAVYNYIQZVWBVWCVYOVYQUSVYRVWAVYNVYPUUGYLVWHVWJUYIUYJVYNTVYNVYPU
      UHUUIXFVVTUWRUXEJQZRZUYHVYQUXIVGUHZUWRUXHVYSUWOUXHVYSYMUWQUWOVYSUXHUWOJUX
      GUXEVYHWIUULYKUUMVYTUYHWUAUWOUWQVYSUYHWUAYMUWOUWQVYSRZRUYHWUAUWOUYHWUBWUA
      UYGUWPUXEVYNIJVYPOUUNUUOUUTUUPUUQWPYNUURXMWQUXLUXIUXAVGUHZUYHUXDUXHUXJWUC
      VVMVYEVYJUXJWUCYMUWRUXHVYEUXCVYIVPUXCVYJUWRUXHUXBVYJUWTVYKUVAUUSUXIUXAUVB
      VQUVCYKYOVURVYCVYJVVRVRUXOUGUHRZVYAVYBYTVYDVYLVURVVFWUDUXDVVFUXKUYHUWOUWT
      VVFUWQUXBVVIXCWEUXOUVDYPVXLUXAUXOUVEXFYQYNVWRUXPUVFXFVURVWSTQZVXEUITQZVXJ
      VXKYTVURVXBVWSXOQWUEVXCVXDVWSXSWCVXIWUFVURUVGYLVWSUXQUIUVHXFYQYOVUSVUTVVB
      UVIZVUQUXSUYOVGUHUYOUXRVGUHRZVUSVUTVVBWUHVUTUYOTQVXFVVBWUHYTVUSUYOXSUXRUV
      JUYOUXRUVKUVLUVMWUGVUTUXSXOQZUXRXOQZVUQWUHYTVUSVUTVVBUVNVUSVUTWUIVVBUXRUV
      OYRVUSVUTWUJVVBUXRUVPYRUYOUXSUXRUVQXFUVRXFUVSGCUXMUXTUYOUYPUYPYSUVTYPUXLV
      UBUWAVUIVUOVUPREUYPGCUXMUYOVWGUWBUYAUYPUSZUYBVUOVUHVUPUXMUXTUYAUYPUWDWUKV
      UGVUBVUBWUKVUFUYTUWSVUAWUKFDUXMVUEUYSWUKVUDUYRUYFUNWUKVUCUYQUXOVAUYCUYAUY
      PUWCYEYEUWEUWFUWGUWHUWIVQVUNVUJMVUBUYTUWSVUAWKVUKVUBUSZVUMVUIEWULVULVUHUY
      BVUKVUBVUGUWJUWKUWLUWNUWM $.
      $( [2-Sep-2009] $)

    $( Lemma for ~ rrntotbnd . $)
    rrntotbndlem2 $p |- ( ( ( N e. NN /\ d e. RR+ ) /\
          ( x e. ( RR ^m ( 1 ... N ) ) /\ y e. ( RR ^m ( 1 ... N ) ) ) ) ->
                  ( { <. m , u >. | ( m e. ( 1 ... N ) /\ u = ( ( x ` m ) +
        ( ( d / ( sqr ` N ) ) x. ( { <. n , v >. | ( n e. ( 1 ... N ) /\ v =
                ( |_ ` ( ( ( ( y ` n ) - ( x ` n ) ) / ( d / ( sqr ` N ) ) ) +
                      ( 1 / 2 ) ) ) ) } ` m ) ) ) ) } ( Rn ` N ) y ) < d ) $=
      ( wcel wa cr co cfv wceq cmul va cn cv crp c1 cfz cmap csqr cdiv c2 caddc
      cmin cfl copab crrn clt cabs ccom cxp cres wral simpll wf reex ovex elmap
      wbr ffvelrn sylanb adantll adantlrr nnrp rpsqrcl syl sylan2 rpre ad2antrr
      rpdivcl ancoms cc0 wne adantlr resubcl syl2anc rpne0 redivcl syl3anc 2ne0
      cz 2re rereccli readdcl sylancl flcl zre 3syl ralrimiva eqid fopab2 sylib
      sylan remulcl sylibr simprr adantr fveq2 oveq2d oveq1d adantl fveq2d fvex
      oveq12d fvopab4 jca simprl remetdval cc recnd subcl abscl a1i 1re halflt1
      cle rddif lelttrd wb rpgt0 ltmul2 syl112anc recn ad2antrl addcom ad2antll
      mpbid subsub3 rpcn divcan2 eqcomd eqbrtrd subdi eqtr4d absmul rpge0 absid
      3eqtr2d 3eqtrd mulid1 3brtr4d anassrs rrndstprj2 syl32anc divcan1 breqtrd
      syl2an ) HUBNZKUCZUDNZOZAUCZPUEHUFQZUGQZNZBUCZUVBNZOZOZEUCZUVANZDUCUVHUUT
      RZUUQHUHRZUIQZUVHFUCZUVANZCUCUVMUVDRZUVMUUTRZULQZUVLUIQZUEUJUIQZUKQZUMRZS
      OFCUNZRZTQZUKQZSOEDUNZUVDHUORQZUVLUVKTQZUUQUPUVGUUPUWFUVBNZUVEUVLUDNZUAUC
      ZUWFRZUWKUVDRZUQULURPPUSUTZQZUVLUPVGZUAUVAVAUWGUWHUPVGUUPUURUVFVBUVGUVAPU
      WFVCZUWIUVGUWEPNZEUVAVAUWQUVGUWREUVAUVGUVIOZUVJPNZUWDPNZUWRUUSUVCUVIUWTUV
      EUVCUVIUWTUUSUVCUVAPUUTVCZUVIUWTPUVAUUTVDUEHUFVEZVFZUVAPUVHUUTVHVIVJVKUWS
      UVLPNZUWCPNZUXAUUSUXEUVFUVIUURUUPUXEUURUUPOZUWJUXEUUPUURUVKUDNZUWJUUPHUDN
      ZUXHHVLZHVMZVNUUQUVKVRVOZUVLVPZVNVSZVQUVGUVAPUWBVCZUVIUXFUVGUWAPNZFUVAVAU
      XOUVGUXPFUVAUVGUVNOZUVTPNZUWAWINUXPUXQUVRPNZUVSPNZUXRUXQUVQPNZUXEUVLVTWAZ
      UXSUVFUVNUYAUUSUVFUVNOUVOPNZUVPPNZUYAUVEUVNUYCUVCUVEUVAPUVDVCZUVNUYCPUVAU
      VDVDUXCVFZUVAPUVMUVDVHVIVJUVCUVNUYDUVEUVCUXBUVNUYDUXDUVAPUVMUUTVHVIWBUVOU
      VPWCWDVJUUSUXEUVFUVNUXNVQUUSUYBUVFUVNUURUUPUYBUXGUWJUYBUXLUVLWEZVNVSVQUVQ
      UVLWFWGUJWJWHWKZUVRUVSWLWMUVTWNUWAWOWPWQFCUVAPUWAUWBUWBWRZWSWTUVAPUVHUWBV
      HXAUVLUWCXBWDUVJUWDWLWDWQEDUVAPUWEUWFUWFWRZWSWTPUVAUWFVDUXCVFXCUUSUVCUVEX
      DUUSUWJUVFUURUUPUWJUXLVSZXEUVGUWPUAUVAUVGUWKUVANZOZUWOUWKUUTRZUVLUWKUWBRZ
      TQZUKQZUWMUWNQZUVLUPUYLUWOUYRSUVGUYLUWLUYQUWMUWNEDUWKUWEUYQUVAUWFUVHUWKSZ
      UVJUYNUWDUYPUKUVHUWKUUTXFUYSUWCUYOUVLTUVHUWKUWBXFXGXLUYJUYNUYPUKVEXMXHXIU
      YMUYRUYNUVLUWMUYNULQZUVLUIQZUVSUKQZUMRZTQZUKQZUWMUWNQZUVLUPUYLUYRVUFSUVGU
      YLUYQVUEUWMUWNUYLUYPVUDUYNUKUYLUYOVUCUVLTFCUWKUWAVUCUVAUWBUVMUWKSZUVTVUBU
      MVUGUVRVUAUVSUKVUGUVQUYTUVLUIVUGUVOUWMUVPUYNULUVMUWKUVDXFUVMUWKUUTXFXLXHX
      HXJUYIVUBUMXKXMXGXGXHXIUUSUVFUYLVUFUVLUPVGZUUSUWJUYNPNZUWMPNZOZVUHUVFUYLO
      ZUYKVULVUIVUJUVCUYLVUIUVEUVCUXBUYLVUIUXDUVAPUWKUUTVHVIWBUVEUYLVUJUVCUVEUY
      EUYLVUJUYFUVAPUWKUVDVHVIVJXNUWJVUKOZVUFVUEUWMULQZUQRZUVLUPVUMVUEPNZVUJVUF
      VUOSVUMVUIVUDPNZVUPUWJVUIVUJXOVUMUXEVUCPNZVUQUWJUXEVUKUXMXEZVUMVUBPNZVUCW
      INVURVUMVUAPNZUXTVUTVUMUYTPNZUXEUYBVVAVUKVVBUWJVUJVUIVVBUWMUYNWCVSXIZVUSU
      WJUYBVUKUYGXEZUYTUVLWFWGZUYHVUAUVSWLWMVUBWNVUCWOWPZUVLVUCXBWDZUYNVUDWLWDU
      WJVUIVUJXDVUEUWMUWNUWNWRZXPWDVUMUVLVUCVUAULQZUQRZTQZUVLUETQZVUOUVLUPVUMVV
      JUEUPVGZVVKVVLUPVGZVUMVVJUVSUEVUMVVIXQNZVVJPNZVUMVUCXQNZVUAXQNZVVOVUMVUCV
      VFXRZVUMVUAVVEXRZVUCVUAXSWDZVVIXTVNZUXTVUMUYHYAUEPNZVUMYBYAZVUMVVAVVJUVSY
      DVGVVEVUAYEVNUVSUEUPVGVUMYCYAYFVUMVVPVWCUXEVTUVLUPVGZVVMVVNYGVWBVWDVUSUWJ
      VWEVUKUVLYHXEVVJUEUVLYIYJYOVUMVUOUVLVVITQZUQRZUVLUQRZVVJTQZVVKVUMVUNVWFUQ
      VUMVUNVUDUYNUKQZUWMULQZVUDUYTULQZVWFVUMVUEVWJUWMULVUMUYNXQNZVUDXQNZVUEVWJ
      SVUIVWMUWJVUJUYNYKYLZVUMVUDVVGXRZUYNVUDYMWDXHVUMVWNUWMXQNZVWMVWLVWKSVWPVU
      JVWQUWJVUIUWMYKYNVWOVUDUWMUYNYPWGVUMVWLVUDUVLVUATQZULQZVWFVUMUYTVWRVUDULV
      UMVWRUYTVUMUYTXQNUVLXQNZUYBVWRUYTSVUMUYTVVCXRUWJVWTVUKUVLYQZXEZVVDUYTUVLY
      RWGYSXGVUMVWTVVQVVRVWFVWSSVXBVVSVVTUVLVUCVUAUUAWGUUBUUFXJVUMVWTVVOVWGVWIS
      VXBVWAUVLVVIUUCWDVUMVWHUVLVVJTVUMUXEVTUVLYDVGZVWHUVLSVUSUWJVXCVUKUVLUUDXE
      UVLUUEWDXHUUGVUMVVLUVLUWJVVLUVLSZVUKUWJVWTVXDVXAUVLUUHVNXEYSUUIYTUUOUUJYT
      YTWQUVLUAUWFUVDUWNHUVBVVHUVBWRUUKUULUUSUWHUUQSZUVFUUSUUQXQNZUVKXQNZUVKVTW
      AZVXEUURVXFUUPUUQYQXIUUPVXGUURUUPUXIUXHVXGUXJUXKUVKYQWPXEUUPVXHUURUUPUXIU
      XHVXHUXJUXKUVKWEWPXEUUQUVKUUMWGXEUUN $.
      $( [2-Sep-2009] $)

    $( A set in Euclidean space is totally bounded iff its is bounded.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    rrntotbnd $p |- ( ( N e. NN /\ Y C_ X ) ->
                                      ( M e. TotBnd <-> M e. Bnd ) ) $=
      ( vx vb vf vm wcel wss wa cv cfv co wceq cfn cr c0 vr vv vy vd va ctotbnd
      vu vz vn cbnd cdm eqid totbndbnd cbl crp wrex wral cuni crrn cxp cres a1i
      cn dmeqd cme rrnmet adantr cfz cmap rrndm syl6eqr sseq2d biimpar metssba2
      c1 syl2anc eqtr4d raleqdv wi 0fin id eqsstrd adantl unieq anbi12d rcla4ev
      0ss raleq wne cdiv cfl caddc wf cmul copab wex cab anim2i anassrs clt wbr
      wb ad2antrr eleq2d simprr syl3anc biimpi reex ovex ffvelrn sylanb adantll
      c2 elmap adantlrr syl sylan2 ancoms adantlr readdcl 3syl ralrimiva fopab2
      cz sylib sylan remulcl syl6bb mpbird ad2ant2rl an32s adantrr sylbid bitri
      ex sstri fzfi mp2an cvv ax-mp ral0 sylancr ralrimivw a1d r19.2z csqr cneg
      jctir ssel2 anim1i anasss biimpa elbl cmin rrntotbndlem1 biimpd imdistani
      syl6reqr eleq2i rrntotbndlem2 nnrp rpsqrcl rpdivcl rpre cc0 resubcl rpne0
      simplr redivcl 2re 2ne0 rereccli sylancl flcl zre elbl2 syl22anc expr imp
      sylanl1 eleq2 eluni2 sylibr ssrdv eqeq1d jca blssp inss1 sseq1 syl5ibrcom
      an4s cin sylbird sstr expcom sylsyld vex eqeq1 anbi2d exbidv fzssuz uzssz
      elab cuz zssre fss mpan2 syl2an oveq1 eqeq2d exlimdv syl5bi jctird df-rex
      ralrimiv anbi1i exbii abbii cpw xpfi pwfi mpbi mapsspw ssfi eqeltrri mpan
      abrexfi syl6 ralrimdva rexlimdvva syl5 expdimp pm2.61dane metres syl5eqel
      isbnd2 sstotbnd 3imtr4d impbid2 ) BVCKZDCLZMZAUFKZAUJKZAAUKZUKZVUFULZUMVU
      BVUFGNZUANZAUNOPZQZUAUOUPZGVUFUQZDUBNZURZLZHNZUCNZUDNZBUSOZUNOZPZQZUCVUTU
      KUKZUPZHVUNUQZMZUBRUPZUDUOUQZVUDVUCVUBVUMVULGDUQZVVIVUBVULGVUFDVUBVUFVUTD
      DUTVAZUKZUKZDVUBVUEVVLVUBAVVKAVVKQVUBFVBVDVDZVUBVUTVEKZDVVDLZDVVMQUYTVVOV
      UABVFZVGZUYTVVPVUAUYTVVDCDUYTVVDSVOBVHPZVIPZCBVJZEVKVLVMZVUTVVDDVVDULZVNV
      PZVQVRVUBVVJVVIVSDTVUBDTQZMZVVIVVJVWFVVHUDUOVWFTRKDTURZLZVVEHTUQZMZVVHVTV
      WFVWHVWIVWEVWHVUBVWEDTVWGVWEWATVWGLVWEVWGWGVBWBWCVVEHUUAUUHVVGVWJUBTRVUNT
      QZVUPVWHVVFVWIVWKVUOVWGDVUNTWDVLVVEHVUNTWHWEWFUUBUUCUUDVUBDTWIZVVJVVIVWLV
      VJMVULGDUPVUBVVIVULGDUUEVUBVUKVVIGUADUOVUBVUHDKZVUIUOKZMZMZVUKVVHUDUOVWPV
      USUOKZMZVUKDVVSVUIVUSBUUFOZWJPZWJPWKOVOWLPZUUGZVXAVHPZINZWMZUENZJNZVVSKZU
      GNZVXGVUHOZVWTVXGVXDOZWNPZWLPZQMJUGWOZVUSVVAPZQZMZIWPZUEWQZURZLZVVEHVXSUQ
      ZMZVVHVWRVUKVYAVYBVWRVUHVUIVVAPZVXTLZVUKDVYDLZVYAVWRUCVYDVXTVWPUYTVUHCKZV
      WNMZMZVWQVURVYDKZVURVXTKZVSZUYTVUAVWOVYIVUAVWOMVYHUYTVUAVWMVWNVYHVUAVWMMZ
      VYGVWNDCVUHUUIZUUJUUKWRWSUYTVWQVYHVYLUYTVYGVWQVWNVYLUYTVYGMZVWQVWNMZMZVYJ
      VURVVDKZVUHVURVUTPVUIWTXAZMZVYKVYQVVOVUHVVDKZVWNVYJVYTXBUYTVVOVYGVYPVVQXC
      VYOWUAVYPUYTVYGWUAUYTCVVDVUHUYTVVDVVTCVWAEUURXDUULVGVYOVWQVWNXEVURVUTVUHV
      UIVVDVWCUUMXFVYQVYTVYKVYQVYTMZVURUHNZKZUHVXSUPZVYKWUBVXHVXIVXJVWTVXGUINZV
      VSKZVUNWUFVUROZWUFVUHOZUUNPZVWTWJPZVOXMWJPZWLPZWKOZQMUIUBWOZOZWNPZWLPZQMJ
      UGWOZVUSVVAPZVXSKVURWUTKZWUEGUCUBUGIJUIABCDUAUEUDEFUUOVYQVYRWVAVYSVYOVWQV
      YRWVAVWNVYOVWQMZVYRMWVBVURVVTKZMWVAWVBVYRWVCUYTVYRWVCVSVYGVWQUYTVYRWVCUYT
      VVDVVTVURVWAXDZUUPXCUUQVYOUYTVUHVVTKZMZVWQWVCWVAVYGWVEUYTVYGWVECVVTVUHEUU
      SZXGWRWVFVWQMWVCWVAUYTVWQWVEWVCWVAVSUYTVWQMZWVEWVCWVAWVHWVEWVCMZMZWVAWUSV
      URVUTPVUSWTXAZGUCUBUGJUIABCDUDEFUUTWVJVVOVWQWUSVVDKZVYRWVAWVKXBUYTVVOVWQW
      VIVVQXCUYTVWQWVIUVHWVJWVLVVSSWUSWMZWVJWURSKZJVVSUQWVMWVJWVNJVVSWVJVXHMZVX
      JSKZWUQSKZWVNWVHWVEVXHWVPWVCWVEVXHWVPWVHWVEVVSSVUHWMZVXHWVPSVVSVUHXHVOBVH
      XIZXNZVVSSVXGVUHXJZXKXLXOWVOVWTSKZWUPSKZWVQWVHWWBWVIVXHVWQUYTWWBVWQUYTMZV
      WTUOKZWWBUYTVWQVWSUOKZWWEUYTBUOKWWFBUVABUVBXPVUSVWSUVCXQZVWTUVDXPXRZXCWVJ
      VVSSWUOWMZVXHWWCWVJWUNSKZUIVVSUQWWIWVJWWJUIVVSWVJWUGMZWUMSKZWUNYDKWWJWWKW
      UKSKZWULSKWWLWWKWUJSKZWWBVWTUVEWIZWWMWVIWUGWWNWVHWVIWUGMWUHSKZWUISKZWWNWV
      CWUGWWPWVEWVCVVSSVURWMWUGWWPSVVSVURXHWVSXNVVSSWUFVURXJXKXLWVEWUGWWQWVCWVE
      WVRWUGWWQWVTVVSSWUFVUHXJXKXSWUHWUIUVFVPXLWVHWWBWVIWUGWWHXCWVHWWOWVIWUGVWQ
      UYTWWOWWDWWEWWOWWGVWTUVGXPXRXCWUJVWTUVIXFXMUVJUVKUVLWUKWULXTUVMWUMUVNWUNU
      VOYAYBUIUBVVSSWUNWUOWUOULYCYEVVSSVXGWUOXJYFVWTWUPYGVPVXJWUQXTVPYBJUGVVSSW
      URWUSWUSULYCYEWVJWVLWUSVVTKWVMWVJVVDVVTWUSUYTVVDVVTQVWQWVIVWAXCXDSVVSWUSX
      HWVSXNYHYIUYTWVCVYRVWQWVEUYTVYRWVCWVDVMYJVURVUTWUSVUSVVDVWCUVPUVQYIUVRYKU
      VSUVTXPXOYLWUDWVAUHWUTVXSWUCWUTVURUWAWFVPUHVURVXSUWBUWCYOYMUWKYKYFUWDVWPV
      UKVYFVSVWQVWPVUKDVUJQZVYFVWPDVUFVUJVUBDVUFQVWOVUBDVVMVUFVWDVVNVQVGUWEVWPV
      YFWWRVUJVYDLZVUBVVOVVPMZVWOWWSVUBVVOVVPVVRVWBUWFWWTVWOMZVUJVYDDUWLZVYDVUI
      DVUTAVVDVUHVWCFUWGWXBVYDLWXAVYDDUWHVBWBYFDVUJVYDUWIUWJUWMVGVYFVYEVYADVYDV
      XTUWNUWOUWPVWRVVEHVXSVUQVXSKVXEVUQVXOQZMZIWPZVWRVVEVXRWXEUEVUQHUWQVXFVUQQ
      ZVXQWXDIWXFVXPWXCVXEVXFVUQVXOUWRUWSUWTUXCVWRWXDVVEIVWRWXDVVEVWRWXDMVXNVVD
      KZWXCVVEVWRVXEWXGWXCVWRVXEMZWXGVVSSVXNWMZWXHVXMSKZJVVSUQWXIWXHWXJJVVSVWRV
      XEVXHWXJVWRVXEVXHMZMWVPVXLSKZWXJVWPVXHWVPVWQVXEVUBVWMVXHWVPVWNVUBVWMMWVRV
      XHWVPUYTVUAVWMWVRVYMUYTVYGWVRVYNVYGWVRUYTVYGWVRVYGWVEWVRWVGWVTYNXGWCXQWSW
      WAYFXOYJVWRWWBVXKSKZWXLWXKVUBVWQWWBVWOUYTVWQWWBVUAWWHXSXSVXEVVSSVXDWMZVXH
      WXMVXEVXCSLWXNVXCYDSVXCVXBUXDOYDVXBVXAUXAVXBUXBYPUXEYPVVSVXCSVXDUXFUXGVVS
      SVXGVXDXJYFVWTVXKYGUXHVXJVXLXTVPWSYBJUGVVSSVXMVXNVXNULYCYEVWPWXGWXIXBZVWQ
      VXEUYTWXOVUAVWOUYTWXGVXNVVTKWXIUYTVVDVVTVXNVWAXDSVVSVXNXHWVSXNYHXCXCYIYLV
      WRVXEWXCXEVVCWXCUCVXNVVDVURVXNQVVBVXOVUQVURVXNVUSVVAUXIUXJWFVPYOUXKUXLUXO
      UXMVXSRKVYCVVHVXPIVXCVVSVIPZUPZUEWQZVXSRWXQVXRUEWXQVXDWXPKZVXPMZIWPVXRVXP
      IWXPUXNWXTVXQIWXSVXEVXPVXCVVSVXDVXBVXAVHXIWVSXNUXPUXQYNUXRWXPRKZWXRRKVVSV
      XCUTZUXSZRKZWXPWYCLZWYAWYBRKZWYDVVSRKVXCRKWYFVOBYQVXBVXAYQVVSVXCUXTYRWYBU
      YAUYBVVSYSKWYEWVSVXCVVSYSUYCYTWYCWXPUYDYRIUEWXPVXOUYGYTUYEVVGVYCUBVXSRVUN
      VXSQZVUPVYAVVFVYBWYGVUOVXTDVUNVXSWDVLVVEHVUNVXSWHWEWFUYFUYHUYIUYJUYKUYLUY
      MYMVUBVVOAVEKVUDVUMXBVVRVVOAVVKVEFVUTDUYNUYOGAVUFUAVUGUYPYAVUBVVOVVPVUCVV
      IXBVVRVWBUCUBVUTAVVDDHUDVWCFUYQVPUYRUYS $.
      $( [28-Feb-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    rrnheibor.1 $e |- X = ( RR ^m ( 1 ... N ) ) $.
    rrnheibor.2 $e |- M = ( ( Rn ` N ) |` ( Y X. Y ) ) $.
    rrnheibor.3 $e |- T = ( MetOpen ` M ) $.
    rrnheibor.4 $e |- U = ( MetOpen ` ( Rn ` N ) ) $.
    $( Heine-Borel theorem for Euclidean space.  A subset of Euclidean space is
       compact iff it is closed and bounded.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    rrnheibor $p |- ( ( N e. NN /\ Y C_ X ) ->
                      ( T e. Comp <-> ( Y e. ( Clsd ` U ) /\ M e. Bnd ) ) ) $=
      ( wcel wss wa cms cfv cme adantr cdm eqid co ccmp ctotbnd ccld rrnmet cxp
      cn cbnd crrn metres syl5eqel syl biantrurd heibor syl6bb eleq1i rrncms cr
      cres cfz cmap rrndm syl6reqr sseq2d biimpa cmsss syl5bb rrntotbnd anbi12d
      c1 bitrd ) DUFKZFELZMZAUAKZCNKZCUBKZMZFBUCOKZCUGKZMVMVNCPKZVNMVQVMVTVNVKV
      TVLVKDUHOZPKZVTDUDWBCWAFFUEURZPHWAFUIUJUKQULCACRRZIWDSUMUNVMVOVRVPVSVOWCN
      KVMVRCWCNHUOVMWABWARRZFWESJVKWANKVLDUPQVKVLFWELVKEWEFVKWEUQVIDUSTUTTEDVAG
      VBVCVDVEVFCDEFGHVGVHVJ $.
      $( [2-Sep-2009] $)
  $}

