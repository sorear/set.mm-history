$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ringnegcl.1 $e |- G = ( 1st ` R ) $.
    ringnegcl.2 $e |- X = ran G $.
    ringnegcl.3 $e |- N = ( inv ` G ) $.
    $( A ring is closed under negation.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    rngonegcl $p |- ( ( R e. RingOps /\ A e. X ) -> ( N ` A ) e. X ) $=
      ( crngo wcel cgr cfv rngogrpo grpoinvcl sylan ) BIJCKJAEJADLEJBCFMACDEGHN
      O $.
      $( [10-Jun-2010] $)

    ${
      ringaddneg.4 $e |- Z = ( Id ` G ) $.
      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      rngoaddneg1 $p |- ( ( R e. RingOps /\ A e. X )
            -> ( A G ( N ` A ) ) = Z ) $=
        ( crngo wcel cgr cfv co wceq rngogrpo grporinv sylan ) BKLCMLAELAADNCOF
        PBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)

      $( Adding the negative in a ring gives zero.  (Contributed by Jeff
         Madsen, 10-Jun-2010.) $)
      rngoaddneg2 $p |- ( ( R e. RingOps /\ A e. X )
             -> ( ( N ` A ) G A ) = Z ) $=
        ( crngo wcel cgr cfv co wceq rngogrpo grpolinv sylan ) BKLCMLAELADNACOF
        PBCGQAFCDEHJIRS $.
        $( [10-Jun-2010] $)
    $}

    ringsub.4 $e |- D = ( /g ` G ) $.
    $( Subtraction in a ring, in terms of addition and negation.  (Contributed
       by Jeff Madsen, 19-Jun-2010.) $)
    rngosub $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                        -> ( A D B ) = ( A G ( N ` B ) ) ) $=
      ( crngo wcel cgr co cfv wceq rngogrpo grpodivval syl3an1 ) DLMENMAGMBGMAB
      COABFPEOQDEHRABCEFGIJKST $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringneg.1 $e |- G = ( 1st ` R ) $.
    ringneg.2 $e |- H = ( 2nd ` R ) $.
    ringneg.3 $e |- X = ran G $.
    ringneg.4 $e |- N = ( inv ` G ) $.
    ringneg.5 $e |- U = ( Id ` H ) $.
    $( Negation in a ring is the same as left multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    rngonegmn1l $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( ( N ` U ) H A ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr crngo cgi c1st rneqi eqtri rngo1cl
      rngonegcl jca rngodir 3exp2 imp42 eqid rngoaddneg1 oveq1d rngolz rngolidm
      an32s eqtrd 3eqtr3rd rngocl 3expa cgr rngogrpo grpoinvid1 syl3an1 mpd3an3
      wb mpbird ) BUAMZAGMZNZAFOCFOZAEPZQZAVMDPZDUBOZQZVKCVLDPZAEPZCAEPZVMDPZVP
      VOVKCGMZVLGMZNZVSWAQZVIWDVJVIWBWCBCEGGDRBUCOZRJDWFHUDUEZILUFZVIWBWCWHCBDF
      GHJKUGSZUHTVIWDVJWEVIWBWCVJWEVIWBWCVJWECVLABDEGHIJUIUJUKUQSVKVSVPAEPVPVKV
      RVPAEVIVRVPQZVJVIWBWJWHCBDFGVPHJKVPULZUMSTUNABDEGVPWKJHIUOURVKVTAVMDABCEG
      IWGLUPUNUSVIVJVMGMZVNVQVGZVKWCWLVIWCVJWITVIWCVJWLVIWCVJWLVLABDEGHIJUTVAUQ
      SVIDVBMVJWLWMBDHVCAVMVPDFGJWKKVDVEVFVH $.
      $( [10-Jun-2010] $)

    $( Negation in a ring is the same as right multiplication by ` -u 1 ` .
       (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    rngonegmn1r $p |- ( ( R e. RingOps /\ A e. X ) ->
                                            ( N ` A ) = ( A H ( N ` U ) ) ) $=
      ( wcel wa cfv co wceq crn mpdan adantr crngo cgi c1st rneqi eqtri rngo1cl
      rngonegcl jca rngodi 3exp2 imp43 rngoaddneg2 oveq2d rngorz eqtrd rngoridm
      eqid 3eqtr3rd wb rngocl mpd3an3 cgr rngogrpo grpoinvid2 syl3an1 mpbird )
      BUAMZAGMZNZAFOACFOZEPZQZVKADPZDUBOZQZVIAVJCDPZEPZVKACEPZDPZVNVMVIVJGMZCGM
      ZNVQVSQZVIVTWAVGVTVHVGWAVTBCEGGDRBUCOZRJDWCHUDUEZILUFZCBDFGHJKUGSTZVGWAVH
      WETUHVGVHVTWAWBVGVHVTWAWBAVJCBDEGHIJUIUJUKSVIVQAVNEPVNVIVPVNAEVGVPVNQZVHV
      GWAWGWECBDFGVNHJKVNUQZULSTUMABDEGVNWHJHIUNUOVIVRAVKDABCEGIWDLUPUMURVGVHVK
      GMZVLVOUSZVGVHVTWIWFAVJBDEGHIJUTVAVGDVBMVHWIWJBDHVCAVKVNDFGJWHKVDVEVAVF
      $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringnegmul.1 $e |- G = ( 1st ` R ) $.
    ringnegmul.2 $e |- H = ( 2nd ` R ) $.
    ringnegmul.3 $e |- X = ran G $.
    ringnegmul.4 $e |- N = ( inv ` G ) $.
    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngoneglmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( ( N ` A ) H B ) ) $=
      ( crngo wcel w3a cfv co wceq wi crn rngonegmn1l cgi c1st rneqi eqtri eqid
      rngo1cl rngonegcl mpdan rngoass 3exp2 3imp 3adant3 oveq1d wa rngocl 3expb
      mpd syldan 3impb 3eqtr4rd ) CLMZAGMZBGMZNZEUAOZFOZAEPZBEPZVFABEPZEPZAFOZB
      EPVIFOZVAVBVCVHVJQZVAVFGMZVBVCVMRRVAVEGMVNCVEEGGDSCUBOZSJDVOHUCUDIVEUEZUF
      VECDFGHJKUGUHVAVNVBVCVMVFABCDEGHIJUIUJUQUKVDVKVGBEVAVBVKVGQVCACVEDEFGHIJK
      VPTULUMVAVBVCVLVJQZVAVBVCUNVIGMZVQVAVBVCVRABCDEGHIJUOUPVICVEDEFGHIJKVPTUR
      USUT $.
      $( [19-Jun-2010] $)

    $( Negation of a product in a ring.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    rngonegrmul $p |- ( ( R e. RingOps /\ A e. X /\ B e. X )
                                -> ( N ` ( A H B ) ) = ( A H ( N ` B ) ) ) $=
      ( crngo wcel w3a co cfv wceq wi crn rngonegmn1r cgi c1st rneqi eqtri eqid
      rngo1cl rngonegcl mpdan rngoass 3exp2 com24 com34 mpd rngocl 3expb syldan
      3imp wa 3impb 3adant2 oveq2d 3eqtr4d ) CLMZAGMZBGMZNZABEOZEUAPZFPZEOZABVI
      EOZEOZVGFPZABFPZEOVCVDVEVJVLQZVCVIGMZVDVEVORRVCVHGMVPCVHEGGDSCUBPZSJDVQHU
      CUDIVHUEZUFVHCDFGHJKUGUHVCVPVEVDVOVCVDVEVPVOVCVDVEVPVOABVICDEGHIJUIUJUKUL
      UMUQVCVDVEVMVJQZVCVDVEURVGGMZVSVCVDVEVTABCDEGHIJUNUOVGCVHDEFGHIJKVRTUPUSV
      FVNVKAEVCVEVNVKQVDBCVHDEFGHIJKVRTUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    ringsubdi.1 $e |- G = ( 1st ` R ) $.
    ringsubdi.2 $e |- H = ( 2nd ` R ) $.
    ringsubdi.3 $e |- X = ran G $.
    ringsubdi.4 $e |- D = ( /g ` G ) $.
    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    rngosubdi $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( A H ( B D C ) ) = ( ( A H B ) D ( A H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan crngo cgn rngonegcl ex 3anim123d imp
      rngodi rngonegrmul 3adant3r2 oveq2d eqtr4d 3adant3r1 rngocl 3adant3r3 jca
      eqid rngosub 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABCFUBPZPZFQZGQZABGQZAC
      GQZVFPZFQZABCDQZGQVJVKDQZVEVIVJAVGGQZFQZVMUTVDVAVBVGHMZNZVIVQRUTVDVSUTVAV
      AVBVBVCVRUTVASUTVBSUTVCVRCEFVFHIKVFUPZUCUDUEUFABVGEFGHIJKUGTVEVLVPVJFUTVA
      VCVLVPRVBACEFGVFHIJKVTUHUIUJUKVEVNVHAGUTVBVCVNVHRVABCDEFVFHIKVTLUQULUJUTV
      DVJHMZVKHMZOVOVMRZVEWAWBUTVAVBWAVCABEFGHIJKUMUNUTVAVCWBVBACEFGHIJKUMUIUOU
      TWAWBWCVJVKDEFVFHIKVTLUQURTUS $.
      $( [19-Jun-2010] $)

    $( Ring multiplication distributes over subtraction.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    rngosubdir $p |- ( ( R e. RingOps /\ ( A e. X /\ B e. X /\ C e. X ) )
                          -> ( ( A D B ) H C ) = ( ( A H C ) D ( B H C ) ) ) $=
      ( wcel w3a wa cfv co wceq idd syldan crngo cgn rngonegcl ex 3anim123d imp
      eqid rngodir rngoneglmul 3adant3r1 oveq2d eqtr4d rngosub 3adant3r3 oveq1d
      rngocl 3adant3r2 jca 3expb 3eqtr4d ) EUAMZAHMZBHMZCHMZNZOZABFUBPZPZFQZCGQ
      ZACGQZBCGQZVGPZFQZABDQZCGQVKVLDQZVFVJVKVHCGQZFQZVNVAVEVBVHHMZVDNZVJVRRVAV
      EVTVAVBVBVCVSVDVDVAVBSVAVCVSBEFVGHIKVGUGZUCUDVAVDSUEUFAVHCEFGHIJKUHTVFVMV
      QVKFVAVCVDVMVQRVBBCEFGVGHIJKWAUIUJUKULVFVOVICGVAVBVCVOVIRVDABDEFVGHIKWALU
      MUNUOVAVEVKHMZVLHMZOVPVNRZVFWBWCVAVBVDWBVCACEFGHIJKUPUQVAVCVDWCVBBCEFGHIJ
      KUPUJURVAWBWCWDVKVLDEFVGHIKWALUMUSTUT $.
      $( [19-Jun-2010] $)
  $}

  ${
    zerdivempx.1 $e |- G = ( 1st ` R ) $.
    zerdivempx.2 $e |- H = ( 2nd ` R ) $.
    zerdivempx.3 $e |- Z = ( Id ` G ) $.
    zerdivempx.4 $e |- X = ran G $.
    zerdivempx.5 $e |- U = ( Id ` H ) $.
    $d A a $.  $d B a $.  $d H a $.  $d R a $.  $d X a $.  $d Z a $.
    $( In a unitary ring a left invertible element is not a zero divisor.
       Generalization of ~ zerdivemp1 by Frederic Line.  (Contributed by Jeff
       Madsen, 18-Apr-2010.) $)
    zerdivemp1x $p |- ( ( R e. RingOps /\ A e. X /\ E. a e. X ( a H A ) = U )
       -> ( B e. X -> ( ( A H B ) = Z -> B = Z ) ) ) $=
      ( wcel co wceq wi w3a wa crngo wrex oveq2 simpl1 simpr1 simpr3 simpl3 jca
      cv 3jca rngoass eqtr ex oveq1 rngorz 3adant3 crn cfv rneqi eqtri rngolidm
      c1st 3adant2 simp1 simp2 simp3 3eqtr3d 3exp com14 com13 sylc com15 eqcoms
      a1d com24 com25 com12 3imp syl6 3imp1 3syl pm2.43i 3exp1 syl5com rexlimiv
      syl ) CUAOZAGOZIUIZAFPZDQZIGUBZBGOZABFPZHQZBHQZRRZWLWHWGWQWKWHWGWQRZRIGWI
      GOZWKWHWRWOWGWMWSWKWHSZWPWOWIWNFPZWIHFPZQZWGWMWTWPRRWNHWIFUCWGXCWMWTWPWGX
      CWMSZWTTZWPXEWGWSWHWMSZTWJBFPZXAQZXEWPRXEWGXFWGXCWMWTUDXEWSWHWMXDWSWKWHUE
      XDWSWKWHUFWGXCWMWTUGUJUHWIABCEFGJKMUKXEXHWPWGXCWMWTXHWPRXHXCWMWTWGWPXHXCX
      GXBQZWMWTWGWPRZRRXHXCXIXGXAXBULUMWTWMXIXJWSWKWHWMXIXJRRZWKWSWHXKRZWKXGDBF
      PZQZWSXLRWJDBFUNXNXIWHWMWSXJXIWHWMWSXJRRRZRXMXGXMXGQZXIXOXPXITXMXBQZXOXMX
      GXBULXQWSWMWHXJWGWSWMWHXQWPWGWSWMWHXQWPRZRZWGWSWMSXBHQZXMBQZXSWGWSXTWMWIC
      EFGHLMJKUOUPWGWMYAWSBCDFGKGEUQCVBURZUQMEYBJUSUTNVAVCWHYAXTXRXQYAXTWHWPXQY
      AXTWHWPRXQYAXTSZWPWHYCXMXBBHXQYAXTVDXQYAXTVEXQYAXTVFVGVNVHVIVJVKVHVLVOWFU
      MVMVPWFVQVRVJVSVLVTVQWAWBWCWDVIVHWEVJVR $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d g h $.
    isdivrng1.1 $e |- G = ( 1st ` R ) $.
    isdivrng1.2 $e |- H = ( 2nd ` R ) $.
    isdivrng1.3 $e |- Z = ( Id ` G ) $.
    isdivrng1.4 $e |- X = ran G $.
    $( The predicate "is a division ring".  (Contributed by Jeff Madsen,
       8-Jun-2010.) $)
    isdrngo1 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\
             ( H |` ( ( X \ { Z } ) X. ( X \ { Z } ) ) ) e. GrpOp ) ) $=
      ( vg vh cdrng wcel cfv cop crngo csn cdif cgr wa c1st c2nd wceq cres wrel
      cxp cv crn df-drngo relopabi 1st2nd mpan relrngo adantr wb opeq12i eqeq2i
      cgi cvv fvex eqeltri isdivrngo ax-mp sneqi difeq12i xpeq12i eleq1i anbi2i
      reseq2i bitr4i eleq1 anbi1d bibi12d mpbiri sylbir pm5.21nii ) ALMZAAUANZA
      UBNZOZUCZAPMZCDEQZRZWDUFZUDZSMZTZLUEVQWAJUGZKUGZOPMWJWIUHWIURNQRZWKUFUDSM
      TJKLJKUIUJALUKULWBWAWGPUEWBWAUMAPUKULUNWAABCOZUCZVQWHUOZWLVTABVRCVSFGUPUQ
      WMWNWLLMZWLPMZWGTZUOWOWPCBUHZBURNZQZRZXAUFZUDZSMZTZWQCUSMWOXEUOCVSUSGAUBU
      TVAUSBCVBVCWGXDWPWFXCSWEXBCWDXAWDXADWRWCWTIEWSHVDVEZXFVFVIVGVHVJWMVQWOWHW
      QAWLLVKWMWBWPWGAWLPVKVLVMVNVOVP $.
      $( [8-Jun-2010] $)

    $( The product of two nonzero elements of a division ring is nonzero.
       (Contributed by Jeff Madsen, 9-Jun-2010.) $)
    divrngcl $p |- ( ( R e. DivRingOps /\ A e. ( X \ { Z } )
                    /\ B e. ( X \ { Z } ) ) -> ( A H B ) e. ( X \ { Z } ) ) $=
      ( wcel cxp wa co wceq adantl cdm wss eleq2d cdrng crngo csn cdif cres cgr
      isdrngo1 ovres crn wi grpocl 3expib grporndm difss xpss12 mp2an wf rngosm
      eqid fdm syl5sseqr ssdmres sylib adantr dmeqd dmxpid syl6eq eqtrd anbi12d
      syl 3imtr3d imp eqeltrrd 3impb syl3an1b ) CUALCUBLZEFGUCZUDZVRMZUEZUFLZNZ
      AVRLZBVRLZABEOZVRLZCDEFGHIJKUGWBWCWDWFWBWCWDNZNABVTOZWEVRWGWHWEPWBABVRVRE
      UHQWBWGWHVRLZWBAVTUIZLZBWJLZNZWHWJLZWGWIWAWMWNUJVPWAWKWLWNABVTWJWJUSUKULQ
      WBWKWCWLWDWBWJVRAWBWJVTRZRZVRWAWJWPPVPVTUMQWBWPVSRVRWBWOVSVPWOVSPZWAVPVSE
      RZSWQVPFFMZVSWRVRFSZWTVSWSSFVQUNZXAVRFVRFUOUPVPWSFEUQWRWSPCDEFHIKURWSFEUT
      VJVAVSEVBVCVDVEVRVFVGVHZTWBWJVRBXBTVIWBWJVRWHXBTVKVLVMVNVO $.
      $( [9-Jun-2010] $)

    $d H x y u v w z $.  $d X x y u v w z $.  $d Z x y u v w z $.
    $d R x y u v w z $.  $d U x y u v w z $.
    isdivrng2.5 $e |- U = ( Id ` H ) $.
    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 8-Jun-2010.) $)
    isdrngo2 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
             A. x e. ( X \ { Z } ) E. y e. ( X \ { Z } ) ( y H x ) = U ) ) ) $=
      ( wcel wa co wceq wrex adantr ovres vu vv vw vz cdrng crngo csn cdif cres
      cxp cgr wne wral isdrngo1 dvrunz sylbir crn cdm grporndm adantl wss difss
      cv xpss12 mp2an wf rngosm fdm syl5sseqr ssdmres sylib dmeqd dmxpid 3eqtrd
      syl a1i eleq2d biimpar cgn cfv grpoinvcl adantll cgi grpolinv cmagm cexid
      eqid cmnd rngomnd mndmgmid sseqtri rngorn1eq syl5sseq rneqi eqtri rngo1cl
      cin c1st eldifsn sylanbrc grpomnd mndismgm exidresid syl31anc eqtrd oveq1
      eqeq1d rcla4ev syl2anc syldan rexeqdv ancoms rexbidva bitrd ralrimiva jca
      wb mpbid cvv fvex eqeltri difexg ax-mp wfn fnssres sylancl eldifi anim12i
      rnex ffn rngocl 3expb sylan2 adantlr oveq2 wi sylan2b adantlrl w3a sylan
      rexbidv rcla4v imdistanri ssrexv zerdivemp1x syl3an3 syl3an2 necon3d impr
      imp an32s ancom2s an42s eqeltrd ralrimivva ffnov 3anim123i rngoass oveq1d
      3adant3 3adantr3 simpr3 simpr1 fovrn 3adant1 oveq2d 3eqtr4d anim1i sylibr
      3adant3r1 adantrr rngolidm adantlrr rcla4va cbvrexv isgrpda impbida bitri
      pm5.32i ) CUENZCUFNZFGHUGZUHZUWCUJZUIZUKNZOZUWADHULZBVCZAVCZFPZDQZBUWCRZA
      UWCUMZOZOZCEFGHIJKLUNZUWAUWFUWOUWAUWFUWOUWGUWHUWNUWGUVTUWHUWQCDEFGHIJLKMU
      OUPZUWGUWMAUWCUWGUWJUWCNZOZUWIUWJUWEPZDQZBUWEUQZRZUWMUWGUWSUWJUXCNZUXDUWG
      UXEUWSUWGUXCUWCUWJUWGUXCUWEURZURZUWDURZUWCUWFUXCUXGQUWAUWEUSUTUWGUXFUWDUW
      GUWDFURZVAZUXFUWDQUWAUXJUWFUWAGGUJZUWDUXIUWCGVAZUXLUWDUXKVAZGUWBVBZUXNUWC
      GUWCGVDVEZUWAUXKGFVFZUXIUXKQCEFGIJLVGZUXKGFVHVOVISUWDFVJVKVLUXHUWCQUWGUWC
      VMVPVNZVQVRUWGUXEOZUWJUWEVSVTZVTZUXCNZUYAUWJUWEPZDQZUXDUWFUXEUYBUWAUWJUWE
      UXTUXCUXCWGZUXTWGZWAWBUXSUYCUWEWCVTZDUWFUXEUYCUYGQUWAUWJUYGUWEUXTUXCUYEUY
      GWGUYFWDWBUWGUYGDQZUXEUWGFWEWFWQNZUWCFUQZVAZDUWCNZUWEWENZUYHUWAUYIUWFUWAF
      WHNUYICFJWIFWJVOSUWAUYKUWFUWAEUQZUWCUYJUWCGUYNUXNLWKCEFJIWLWMSUWGDGNZUWHU
      YLUWAUYOUWFCDFGGUYNCWRVTZUQLEUYPIWNWOZJMWPZSUWRDGHWSZWTUWFUYMUWAUWFUWEWHN
      UYMUWEXAUWEXBVOUTDFUWEUYJUWCUYJWGMUWEWGXCXDSXEUXBUYDBUYAUXCUWIUYAQUXAUYCD
      UWIUYAUWJUWEXFXGXHXIXJUWTUXDUXBBUWCRZUWMUWTUXBBUXCUWCUWGUXCUWCQUWSUXRSXKU
      WSUYTUWMXQUWGUWSUXBUWLBUWCUWSUWIUWCNZOUXAUWKDVUAUWSUXAUWKQUWIUWJUWCUWCFTX
      LXGXMUTXNXRXOXPUWPUAUBUCDUDUWEUWCUWCXSNZUWPGXSNVUBGUYNXSLEEUYPXSICWRXTYAY
      IYAGUWBXSYBYCVPUWPUWEUWDYDZUAVCZUBVCZUWEPZUWCNZUBUWCUMUAUWCUMUWDUWCUWEVFZ
      UWPFUXKYDZUXMVUCUWAVUIUWOUWAUXPVUIUXQUXKGFYJVOSUXOUXKUWDFYEYFUWPVUGUAUBUW
      CUWCUWPVUDUWCNZVUEUWCNZOZOZVUFVUDVUEFPZUWCVULVUFVUNQZUWPVUDVUEUWCUWCFTZUT
      VUMVUNGNZVUNHULZVUNUWCNZUWAVULVUQUWOVULUWAVUDGNZVUEGNZOVUQVUJVUTVUKVVAVUD
      GUWBYGZVUEGUWBYGZYHUWAVUTVVAVUQVUDVUECEFGIJLYKYLYMYNUWAUWNVULVURUWHUWAVUK
      UWNVUJVURUWNVUJOUWAVUKOZUWIVUDFPZDQZBUWCRZVUJOVURVUJUWNVVGUWMVVGAVUDUWCUW
      JVUDQZUWLVVFBUWCVVHUWKVVEDUWJVUDUWIFYOXGUUAZUUBUUCVVDVUJVVGVURUWAVUJVVGOZ
      VUKVURVUKUWAVVJOZVVAVUEHULZOVURVUEGHWSVVKVVAVVLVURVVKVVAOVUNHVUEHVVKVVAVU
      NHQVUEHQYPZUWAVUJVVGVVAVVMYPZVUJUWAVUTVVGVVNVVBVVGUWAVUTVVFBGRZVVNUXLVVGV
      VOYPUXNVVFBUWCGUUDYCVUDVUECDEFGHBIJKLMUUEUUFUUGYLUUJUUHUUIYQUUKUULYMUUMYR
      VUNGHWSWTZUUNUUOUAUBUWCUWCUWCUWEUUPWTZUWPVUJVUKUCVCZUWCNZYSZOZVUNVVRFPZVU
      DVUEVVRFPZFPZVUFVVRUWEPZVUDVUEVVRUWEPZUWEPZUWAVVTVWBVWDQZUWOVVTUWAVUTVVAV
      VRGNZYSVWHVUJVUTVUKVVAVVSVWIVVBVVCVVRGUWBYGUUQVUDVUEVVRCEFGIJLUURYMYNVWAV
      WEVUNVVRUWEPZVWBVWAVUFVUNVVRUWEVVTVUOUWPVUJVUKVUOVVSVUPUUTUTUUSVWAVUSVVSV
      WJVWBQUWPVUJVUKVUSVVSVVPUVAUWPVUJVUKVVSUVBVUNVVRUWCUWCFTXIXEVWAVWGVUDVWFF
      PZVWDVWAVUJVWFUWCNZVWGVWKQUWPVUJVUKVVSUVCUWPVUHVVTVWLVVQVUHVUKVVSVWLVUJVU
      EVVRUWCUWCUWCUWEUVDUVJYTVUDVWFUWCUWCFTXIVWAVWFVWCVUDFVVTVWFVWCQZUWPVUKVVS
      VWMVUJVUEVVRUWCUWCFTUVEUTUVFXEUVGUWAUWHUYLUWNUWAUWHOZUYOUWHOUYLUWAUYOUWHU
      YRUVHUYSUVIZUVKUWAUWHVUJDVUDUWEPZVUDQUWNVWNVUJOVWPDVUDFPZVUDVWNUYLVUJVWPV
      WQQVWODVUDUWCUWCFTYTUWAVUJVWQVUDQZUWHVUJUWAVUTVWRVVBVUDCDFGJUYQMUVLYMYNXE
      UVMUWAUWNVUJUDVCZVUDUWEPZDQZUDUWCRZUWHUWNVUJVXBUWAVUJUWNVXBVUJUWNVVGVXBUW
      MVVGAVUDUWCVVIUVNVVGVUJVWSVUDFPZDQZUDUWCRZVXBVVFVXDBUDUWCUWIVWSQVVEVXCDUW
      IVWSVUDFXFXGUVOVUJVXBVXEVUJVXAVXDUDUWCVWSUWCNZVUJVXAVXDXQVXFVUJOVWTVXCDVW
      SVUDUWCUWCFTXGXLXMVRYQXJXLWBYRUVPUVQUVSUVR $.
      $( [8-Jun-2010] $)

    $( A division ring is a ring in which ` 1 =/= 0 ` and every nonzero element
       is invertible.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    isdrngo3 $p |- ( R e. DivRingOps <-> ( R e. RingOps /\ ( U =/= Z /\
                        A. x e. ( X \ { Z } ) E. y e. X ( y H x ) = U ) ) ) $=
      ( wcel wne cv co wceq wrex wa cdrng crngo csn cdif isdrngo2 wb eldifi wss
      wral wi difss ssrexv ax-mp neeq1 biimparc rngolz oveq1 syl5ibrcom necon3d
      eqeq1d sylan2 an4s anassrs pm3.2 syl5com eldifsn syl6ibr imdistanda ancom
      imp 3imtr4g reximdv2 impbid2 ralbidva pm5.32da pm5.32i bitri ) CUANCUBNZD
      HOZBPZAPZFQZDRZBGHUCZUDZSZAWEUIZTZTVRVSWCBGSZAWEUIZTZTABCDEFGHIJKLMUEVRWH
      WKVRVSWGWJVRVSTZWFWIAWEWAWENWLWAGNZWFWIUFWAGWDUGWLWMTZWFWIWEGUHWFWIUJGWDU
      KWCBWEGULUMWNWCWCBGWEWNWCVTGNZTWCVTWENZTWOWCTWPWCTWNWCWOWPWNWCTZWOWOVTHOZ
      TZWPWQWRWOWSWLWMWCWRVRWMVSWCWRVSWCTVRWMTZWBHOZWRWCXAVSWBDHUNUOWTXAWRWTVTH
      WBHWTWBHRVTHRZHWAFQZHRWACEFGHKLIJUPXBWBXCHVTHWAFUQUTURUSVJVAVBVCWOWRVDVEV
      TGHVFVGVHWOWCVIWPWCVIVKVLVMVAVNVOVPVQ $.
      $( [10-Jun-2010] $)
  $}

