$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ideals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Idl $.
  $c PrIdl $.
  $c MaxIdl $.

  $( Extend class notation with the class of ideals. $)
  cidl $a class Idl $.

  $( Extend class notation with the class of prime ideals. $)
  cpridl $a class PrIdl $.

  $( Extend class notation with the class of maximal ideals. $)
  cmaxidl $a class MaxIdl $.

  ${
    $d r s i x y z $.
    $( Define the class of (two-sided) ideals of a ring ` R ` .  A subset of
       ` R ` is an ideal if it contains ` 0 ` , is closed under addition, and
       is closed under multiplication on either side by any element of
       ` R ` . $)
    df-idl $a |- Idl = { <. r , s >. | ( r e. RingOps /\ s =
                { i e. ~P ran ( 1st ` r ) | ( ( Id ` ( 1st ` r ) ) e. i /\
                      A. x e. i ( A. y e. i ( x ( 1st ` r ) y ) e. i /\
                    A. z e. ran ( 1st ` r ) ( ( z ( 2nd ` r ) x ) e. i /\
                            ( x ( 2nd ` r ) z ) e. i ) ) ) } ) } $.
  $}

  ${
    $d r s i a b x y $.
    $( Define the class of prime ideals of a ring ` R ` .  A proper ideal ` I `
       of ` R ` is prime if whenever ` A B C_ I ` for ideals ` A ` and ` B ` ,
       either ` A C_ I ` or ` B C_ I ` .  The more familiar definition using
       elements rather than ideals is equivalent provided ` R ` is commutative;
       see ~ ispridl2 and ~ ispridlc . $)
    df-pridl $a |- PrIdl = { <. r , s >. | ( r e. RingOps /\ s =
        { i e. ( Idl ` r ) | ( i =/= ran ( 1st ` r ) /\ A. a e. ( Idl ` r )
          A. b e. ( Idl ` r ) ( A. x e. a A. y e. b ( x ( 2nd ` r ) y ) e. i ->
                                      ( a C_ i \/ b C_ i ) ) ) } ) } $.
  $}

  ${
    $d r s i j $.
    $( Define the class of maximal ideals of a ring ` R ` .  A proper ideal is
       called maximal if it is maximal with respect to inclusion among proper
       ideals. $)
    df-maxidl $a |- MaxIdl = { <. r , s >. | ( r e. RingOps /\ s =
            { i e. ( Idl ` r ) | ( i =/= ran ( 1st ` r ) /\ A. j e. ( Idl ` r )
                      ( i C_ j -> ( j = i \/ j = ran ( 1st ` r ) ) ) ) } ) } $.
  $}

  ${
    $d R r s x y z i $.  $d X r s z i $.  $d I x y z i $.  $d Z r s i $.
    $d G r s i $.  $d H r s i $.
    idlval.1 $e |- G = ( 1st ` R ) $.
    idlval.2 $e |- H = ( 2nd ` R ) $.
    idlval.3 $e |- X = ran G $.
    idlval.4 $e |- Z = ( Id ` G ) $.
    $( The class of ideals of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    idlval $p |- ( R e. RingOps -> ( Idl ` R ) = { i e. ~P X | ( Z e. i /\
                                A. x e. i ( A. y e. i ( x G y ) e. i /\
                      A. z e. X ( ( z H x ) e. i /\ ( x H z ) e. i ) ) ) } ) $=
      ( cv c1st cfv wcel co wral wa vr vs cgi c2nd crn cpw crab crngo cidl wceq
      fveq2 syl6eqr rneqd pweqd fveq2d eleq1d oveqd ralbidv raleqbidv rabeqbidv
      anbi12d df-idl cvv fvex eqeltri rnex pwex rabex fvopab4 ) UAUBDUANZOPZUCP
      ZENZQZANZBNZVKRZVMQZBVMSZCNZVOVJUDPZRZVMQZVOVTWARZVMQZTZCVKUEZSZTZAVMSZTZ
      EWGUFZUGIVMQZVOVPFRZVMQZBVMSZVTVOGRZVMQZVOVTGRZVMQZTZCHSZTZAVMSZTZEHUFZUG
      UHUIVJDUJZWKXEEWLXFXGWGHXGWGFUEZHXGVKFXGVKDOPZFVJDOUKJULZUMLULZUNXGVNWMWJ
      XDXGVLIVMXGVLFUCPIXGVKFUCXJUOMULUPXGWIXCAVMXGVSWPWHXBXGVRWOBVMXGVQWNVMXGV
      KFVOVPXJUQUPURXGWFXACWGHXKXGWCWRWEWTXGWBWQVMXGWAGVTVOXGWADUDPGVJDUDUKKULZ
      UQUPXGWDWSVMXGWAGVOVTXLUQUPVAUSVAURVAUTABCEUBUAVBXEEXFHHXHVCLFFXIVCJDOVDV
      EVFVEVGVHVI $.
      $( [10-Jun-2010] $)

    $( The predicate "is an ideal of the ring ` R ` ."  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    isidl $p |- ( R e. RingOps -> ( I e. ( Idl ` R ) <-> ( I C_ X /\ Z e. I /\
                                A. x e. I ( A. y e. I ( x G y ) e. I /\
                      A. z e. X ( ( z H x ) e. I /\ ( x H z ) e. I ) ) ) ) ) $=
      ( vi wcel cv co wral wa eleq2 cidl cfv cpw crab wss w3a idlval eleq2d crn
      crngo c1st fvex eqeltri rnex elpw2 anbi1i wceq raleqbi1dv anbi12d ralbidv
      cvv elrab 3anass 3bitr4i syl6bb ) DUJOZGDUAUBZOGINPZOZAPZBPEQZVHOZBVHRZCP
      ZVJFQZVHOZVJVNFQZVHOZSZCHRZSZAVHRZSZNHUCZUDZOZGHUEZIGOZVKGOZBGRZVOGOZVQGO
      ZSZCHRZSZAGRZUFZVFVGWEGABCDNEFHIJKLMUGUHGWDOZWHWPSZSWGWSSWFWQWRWGWSGHHEUI
      VALEEDUKUBVAJDUKULUMUNUMUOUPWCWSNGWDVHGUQZVIWHWBWPVHGITWAWOAVHGWTVMWJVTWN
      VLWIBVHGVHGVKTURWTVSWMCHWTVPWKVRWLVHGVOTVHGVQTUSUTUSURUSVBWGWHWPVCVDVE $.
      $( [10-Jun-2010] $)

    $d X x $.
    $( The predicate "is an ideal of the commutative ring ` R ` ."
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    isidlc $p |- ( R e. CRing -> ( I e. ( Idl ` R ) <-> ( I C_ X /\ Z e. I /\
                                A. x e. I ( A. y e. I ( x G y ) e. I /\
                                    A. z e. X ( z H x ) e. I ) ) ) ) $=
      ( wcel cv co wral wa w3a wb ccring cidl cfv wss crngo crngorngo isidl syl
      ssel2 wi crngcom eleq1d biimprd 3expa pm4.71 sylib bicomd ralbidva anbi2d
      sylan2 anassrs adantrr pm5.32da df-3an 3bitr4g bitrd ) DUANZGDUBUCNZGHUDZ
      IGNZAOZBOEPGNBGQZCOZVKFPZGNZVKVMFPZGNZRZCHQZRZAGQZSZVIVJVLVOCHQZRZAGQZSZV
      GDUENVHWBTDUFABCDEFGHIJKLMUGUHVGVIVJRZWARWGWERWBWFVGWGWAWEVGVIWAWETVJVGVI
      RVTWDAGVGVIVKGNZVTWDTZVIWHRVGVKHNZWIGHVKUIVGWJRZVSWCVLWKVRVOCHWKVMHNZRZVO
      VRWMVOVQUJZVOVRTVGWJWLWNVGWJWLSZVQVOWOVPVNGVKVMDEFHJKLUKULUMUNVOVQUOUPUQU
      RUSUTVAURVBVCVIVJWAVDVIVJWEVDVEVF $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d I x y z $.  $d X z $.
    idlss.1 $e |- G = ( 1st ` R ) $.
    idlss.2 $e |- X = ran G $.
    $( An ideal of ` R ` is a subset of ` R ` .  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    idlss $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) ) -> I C_ X ) $=
      ( vx vy vz crngo wcel cidl cfv wa wss cgi cv co wral eqid c2nd w3a biimpa
      isidl simp1d ) AJKZCALMKZNCDOZBPMZCKZGQZHQBRCKHCSIQZUKAUAMZRCKUKULUMRCKNI
      DSNGCSZUFUGUHUJUNUBGHIABUMCDUIEUMTFUITUDUCUE $.
      $( [10-Jun-2010] $)

    $( An element of an ideal is an element of the ring.  (Contributed by Jeff
       Madsen, 19-Jun-2010.) $)
    idlcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                                      /\ A e. I ) -> A e. X ) $=
      ( crngo wcel cidl cfv wa idlss sselda ) BHIDBJKILDEABCDEFGMN $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d I x y z $.  $d G z $.
    idl0cl.1 $e |- G = ( 1st ` R ) $.
    idl0cl.2 $e |- Z = ( Id ` G ) $.
    $( An ideal contains ` 0 ` .  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    idl0cl $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) ) -> Z e. I ) $=
      ( vx vy vz crngo wcel cidl cfv wa crn wss cv co wral eqid c2nd w3a biimpa
      isidl simp2d ) AJKZCALMKZNCBOZPZDCKZGQZHQBRCKHCSIQZUKAUAMZRCKUKULUMRCKNIU
      HSNGCSZUFUGUIUJUNUBGHIABUMCUHDEUMTUHTFUDUCUE $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d I x y z $.  $d G x y z $.  $d A x y $.  $d B y $.
    idladdcl.1 $e |- G = ( 1st ` R ) $.
    $( An ideal is closed under addition.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    idladdcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                              /\ ( A e. I /\ B e. I ) ) -> ( A G B ) e. I ) $=
      ( vx vy vz crngo wcel cidl cfv wa cv co wral eqid wceq eleq1d crn wss cgi
      c2nd w3a isidl biimpa simp3d simpl ralimi syl oveq1 oveq2 rcla42v mpan9 )
      CJKZECLMKZNZGOZHOZDPZEKZHEQZGEQZAEKBEKNABDPZEKZURVCIOZUSCUDMZPEKUSVGVHPEK
      NIDUAZQZNZGEQZVDUREVIUBZDUCMZEKZVLUPUQVMVOVLUEGHICDVHEVIVNFVHRVIRVNRUFUGU
      HVKVCGEVCVJUIUJUKVBVFAUTDPZEKGHABEEUSASVAVPEUSAUTDULTUTBSVPVEEUTBADUMTUNU
      O $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d I x y z $.  $d X x z $.  $d H x z $.  $d A x z $.
    $d B z $.
    idllmulcl.1 $e |- G = ( 1st ` R ) $.
    idllmulcl.2 $e |- H = ( 2nd ` R ) $.
    idllmulcl.3 $e |- X = ran G $.
    $( An ideal is closed under multiplication on the left.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    idllmulcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                              /\ ( A e. I /\ B e. X ) ) -> ( B H A ) e. I ) $=
      ( vz vx vy wcel cfv wa cv co wral ralimi crngo cidl wss eqid isidl biimpa
      cgi w3a simp3d simpl adantl syl wceq oveq2 eleq1d oveq1 rcla42v mpan9 ) C
      UANZFCUBONZPZKQZLQZERZFNZKGSZLFSZAFNBGNPBAERZFNZVAVCMQDRFNMFSZVEVCVBERFNZ
      PZKGSZPZLFSZVGVAFGUCZDUGOZFNZVOUSUTVPVRVOUHLMKCDEFGVQHIJVQUDUEUFUIVNVFLFV
      MVFVJVLVEKGVEVKUJTUKTULVEVIVBAERZFNLKABFGVCAUMVDVSFVCAVBEUNUOVBBUMVSVHFVB
      BAEUPUOUQUR $.
      $( [10-Jun-2010] $)

    $( An ideal is closed under multiplication on the right.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    idlrmulcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                              /\ ( A e. I /\ B e. X ) ) -> ( A H B ) e. I ) $=
      ( vx vz vy wcel cfv wa cv co wral ralimi crngo cidl wss eqid isidl biimpa
      cgi w3a simp3d simpr adantl syl wceq oveq1 eleq1d oveq2 rcla42v mpan9 ) C
      UANZFCUBONZPZKQZLQZERZFNZLGSZKFSZAFNBGNPABERZFNZVAVBMQDRFNMFSZVCVBERFNZVE
      PZLGSZPZKFSZVGVAFGUCZDUGOZFNZVOUSUTVPVRVOUHKMLCDEFGVQHIJVQUDUEUFUIVNVFKFV
      MVFVJVLVELGVKVEUJTUKTULVEVIAVCERZFNKLABFGVBAUMVDVSFVBAVCEUNUOVCBUMVSVHFVC
      BAEUPUOUQUR $.
      $( [10-Jun-2010] $)
  $}

  ${
    idlnegcl.1 $e |- G = ( 1st ` R ) $.
    idlnegcl.2 $e |- N = ( inv ` G ) $.
    $( An ideal is closed under negation.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    idlnegcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                                          /\ A e. I ) -> ( N ` A ) e. I ) $=
      ( crngo wcel cidl cfv wa c2nd cgi co crn wss eqid anassrs mpdan wceq c1st
      idlss ssel2 rngonegmn1l sylan2 syldanl rneqi rngonegcl ad2antrr idllmulcl
      rngo1cl eqeltrd ) BHIZDBJKIZLZADIZLZAEKZBMKZNKZEKZAUTOZDUNUODCPZQZUQUSVCU
      AZBCDVDFVDRZUCUNVEUQVFVEUQLUNAVDIVFDVDAUDABVACUTEVDFUTRZVGGVARZUEUFSUGURV
      BVDIZVCDIZUNVJUOUQUNVAVDIVJBVAUTVDCBUBKFUHVHVIULVABCEVDFVGGUITUJUPUQVJVKA
      VBBCUTDVDFVHVGUKSTUM $.
      $( [10-Jun-2010] $)
  $}

  ${
    idlsubcl.1 $e |- G = ( 1st ` R ) $.
    idlsubcl.2 $e |- D = ( /g ` G ) $.
    $( An ideal is closed under subtraction.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    idlsubcl $p |- ( ( ( R e. RingOps /\ I e. ( Idl ` R ) )
                              /\ ( A e. I /\ B e. I ) ) -> ( A D B ) e. I ) $=
      ( crngo wcel cidl cfv wa co cgn crn wceq eqid idlcl syldan anim12da 3expb
      rngosub adantlr simprl idlnegcl adantrl jca idladdcl eqeltrd ) DIJZFDKLJZ
      MZAFJZBFJZMZMZABCNZABEOLZLZENZFUMUPAEPZJZBVBJZMZURVAQZUMUNUOVCVDADEFVBGVB
      RZSBDEFVBGVGSUAUKVEVFULUKVCVDVFABCDEUSVBGVGUSRZHUCUBUDTUMUPUNUTFJZMVAFJUQ
      UNVIUMUNUOUEUMUOVIUNBDEFUSGVHUFUGUHAUTDEFGUITUJ $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d X x y z $.
    rngidl.1 $e |- G = ( 1st ` R ) $.
    rngidl.2 $e |- X = ran G $.
    $( A ring ` R ` is an ` R ` ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    rngoidl $p |- ( R e. RingOps -> X e. ( Idl ` R ) ) $=
      ( vx vy vz crngo wcel cfv cv co wral wa eqid 3expa ralrimiva rngocl jca
      cidl wss cgi c2nd isidl ssid a1i rngo0cl rngogcl w3a 3com23 mpbir3and ) A
      IJZCAUAKJCCUBZBUCKZCJFLZGLZBMCJZGCNZHLZUPAUDKZMCJZUPUTVAMCJZOZHCNZOZFCNFG
      HABVACCUODVAPZEUOPZUEUNUMCUFUGABCUODEVHUHUMVFFCUMUPCJZOZUSVEVJURGCUMVIUQC
      JURUPUQABCDEUIQRVJVDHCUMVIUTCJZVDUMVIVKUJVBVCUMVKVIVBUTUPABVACDVGESUKUPUT
      ABVACDVGESTQRTRUL $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x y z $.  $d Z x y z $.  $d G z $.
    0idl.1 $e |- G = ( 1st ` R ) $.
    0idl.2 $e |- Z = ( Id ` G ) $.
    $( The set containing only ` 0 ` is an ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    0idl $p |- ( R e. RingOps -> { Z } e. ( Idl ` R ) ) $=
      ( vx vy vz wcel cfv cv co wral wa eqid wceq ovex elsnc sylibr eleq1d cidl
      crngo csn crn wss c2nd isidl rngo0cl snssd cgi cvv fvex eqeltri snid elsn
      a1i rngo0rid mpdan syl5ibrcom syl5bi ralrimiv rngorz rngolz jca ralrimiva
      oveq2 oveq1 ralbidv anbi12d mpbir3and ) AUBIZCUCZAUAJIVLBUDZUECVLIZFKZGKZ
      BLZVLIZGVLMZHKZVOAUFJZLZVLIZVOVTWALZVLIZNZHVMMZNZFVLMFGHABWAVLVMCDWAOZVMO
      ZEUGVKCVMABVMCDWJEUHZUIVNVKCCBUJJUKEBUJULUMUNUPVKWHFVLVOVLIVOCPZVKWHFCUOV
      KWHWLCVPBLZVLIZGVLMZVTCWALZVLIZCVTWALZVLIZNZHVMMZNVKWOXAVKWNGVLVPVLIVPCPZ
      VKWNGCUOVKWNXBCCBLZVLIZVKXCCPZXDVKCVMIXEWKCABVMCDWJEUQURXCCCCBQRSXBWMXCVL
      VPCCBVFTUSUTVAVKWTHVMVKVTVMINZWQWSXFWPCPWQVTABWAVMCEWJDWIVBWPCVTCWAQRSXFW
      RCPWSVTABWAVMCEWJDWIVCWRCCVTWAQRSVDVEVDWLVSWOWGXAWLVRWNGVLWLVQWMVLVOCVPBV
      GTVHWLWFWTHVMWLWCWQWEWSWLWBWPVLVOCVTWAVFTWLWDWRVLVOCVTWAVGTVIVHVIUSUTVAVJ
      $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x $.  $d X x $.  $d I x $.  $d U x $.
    1idl.1 $e |- G = ( 1st ` R ) $.
    1idl.2 $e |- H = ( 2nd ` R ) $.
    1idl.3 $e |- X = ran G $.
    1idl.4 $e |- U = ( Id ` H ) $.
    $( Two ways of expressing the unit ideal.  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    1idl $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) )
           -> ( U e. I <-> I = X ) ) $=
      ( vx crngo wcel cidl cfv wa wceq wss adantr crn idlss cv c1st rneqi eqtri
      rngolidm ad2ant2rl idlrmulcl eqeltrrd expr ssrdv eqssd rngo1cl syl5ibrcom
      co ex eleq2 impbid ) ALMZEANOMZPZBEMZEFQZVAVBVCVAVBPZEFVAEFRVBACEFGIUASVD
      KFEVAVBKUBZFMZVEEMVAVBVFPPBVEDUOZVEEUSVFVGVEQUTVBVEABDFHFCTAUCOZTICVHGUDU
      EZJUFUGBVEACDEFGHIUHUIUJUKULUPVAVBVCBFMZUSVJUTABDFVIHJUMSEFBUQUNUR $.
      $( [10-Jun-2010] $)
  $}

  ${
    0ring.1 $e |- G = ( 1st ` R ) $.
    0ring.2 $e |- H = ( 2nd ` R ) $.
    0ring.3 $e |- X = ran G $.
    0ring.4 $e |- Z = ( Id ` G ) $.
    0ring.5 $e |- U = ( Id ` H ) $.
    $( In a ring, ` 0 = 1 ` iff the ring contains only ` 0 ` .  (Contributed by
       Jeff Madsen, 6-Jan-2011.) $)
    0rngo $p |- ( R e. RingOps -> ( Z = U <-> X = { Z } ) ) $=
      ( crngo wcel wceq csn cgi cfv cvv fvex crn eqeltri snid cidl wb 0idl 1idl
      eleq1 mpbii mpdan syl5ib eqcom syl6ib c1st rneqi eqtri eleq2 elsni eqcomd
      rngo1cl syl6bi syl5com impbid ) ALMZFBNZEFOZNZVCVDVEENZVFVDBVEMZVCVGVDFVE
      MVHFFCPQRJCPSUAUBFBVEUGUHVCVEAUCQMVHVGUDACFGJUEABCDVEEGHIKUFUIUJVEEUKULVC
      BEMZVFVDABDEECTAUMQZTICVJGUNUOHKUSVFVIVHVDEVEBUPVHBFBFUQURUTVAVB $.
      $( [6-Jan-2011] $)
  $}

  ${
    $d R i x y z $.  $d H i x y z $.  $d X i x y z $.  $d U i x y z $.
    $d Z i x y z $.
    divrngidl.1 $e |- G = ( 1st ` R ) $.
    divrngidl.2 $e |- H = ( 2nd ` R ) $.
    divrngidl.3 $e |- X = ran G $.
    divrngidl.4 $e |- Z = ( Id ` G ) $.
    $( The only ideals in a division ring are the zero ideal and the unit
       ideal.  (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    divrngidl $p |- ( R e. DivRingOps -> ( Idl ` R ) = { { Z } , X } ) $=
      ( vy vx vi vz wcel cgi wne cv wceq wa wi cdrng crngo cfv co csn cdif wrex
      wral cidl cpr eqid isdrngo2 idl0cl adantr wex wss fvex eqeltri snss necom
      wo cvv c0 pssdifn0 n0 sylib syl2anb idlss ssdif sselda sylan oveq2 eqeq1d
      rexbidv rcla4va eldifi idllmulcl 1idl biimpd eleq1 imbi1d syl5ibrcom mpid
      anim12i sylan2 anassrs rexlimdva imp syldan an32s exlimdv syl5 mpand neor
      ex sylibr 0idl rngoidl jaod impbid vex elpr syl6bbr eqrdv adantrl sylbi )
      AUANAUBNZCOUCZEPZJQZKQZCUDZXHRZJDEUEZUFZUGZKXOUHZSSAUIUCZXNDUJZRZKJAXHBCD
      EFGIHXHUKZULXGXQXTXIXGXQSZLXRXSYBLQZXRNZYCXNRZYCDRZVAZYCXSNYBYDYGYBYDYGYB
      YDSYCXNPZYFTZYGXGYDXQYIXGYDSZXQSZEYCNZYHYFYJYLXQABYCEFIUMUNYLYHSMQZYCXNUF
      ZNZMUOZYKYFYLXNYCUPZXNYCPZYPYHEYCEBOUCVBIBOUQURUSYCXNUTYQYRSYNVCPYPXNYCVD
      MYNVEVFVGYKYOYFMYKYOYFYJYOXQYFYJYOSZXQXJYMCUDZXHRZJXOUGZYFYSYMXONZXQUUBYJ
      YCDUPZYOUUCABYCDFHVHUUDYNXOYMYCDXNVIVJVKXPUUBKYMXOXKYMRZXMUUAJXOUUEXLYTXH
      XKYMXJCVLVMVNVOVKYSUUBYFYSUUAYFJXOYJYOXJXONZUUAYFTZYOUUFSYJYMYCNZXJDNZSZU
      UGYOUUHUUFUUIYMYCXNVPXJDXNVPWDYJUUJSZUUAYTYCNZYFYMXJABCYCDFGHVQUUKUULYFTU
      UAXHYCNZYFTZYJUUNUUJYJUUMYFAXHBCYCDFGHYAVRVSUNUUAUULUUMYFYTXHYCVTWAWBWCWE
      WFWGWHWIWJWOWKWLWMWJYFYCXNWNWPWOXGYGYDTXQXGYEYDYFXGYDYEXNXRNABEFIWQYCXNXR
      VTWBXGYDYFDXRNABDFHWRYCDXRVTWBWSUNWTYCXNDLXAXBXCXDXEXF $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R i x y z $.  $d C i x y z $.
    $( The intersection of a nonempty collection of ideals is an ideal.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    intidl $p |- ( ( R e. RingOps /\ C =/= (/) /\ C C_ ( Idl ` R ) )
                                                  -> |^| C e. ( Idl ` R ) ) $=
      ( vx vy vz vi wcel cfv cv co wral wa eqid sylan2 anassrs ralrimiva sylibr
      wss elint2 ex crngo c0 wne cidl w3a cint c1st crn cgi c2nd isidl 3ad2ant1
      cuni intssuni 3ad2ant2 ssel2 idlss 3adant2 unissb sstrd idl0cl vex r19.26
      wb fvex wi idladdcl ralimdva ovex syl6ibr syl5bir expdimp syl5bi ralrimiv
      idllmulcl anass1rs an32s an4s imp idlrmulcl jca mpbir3and ) BUAGZAUBUCZAB
      UDHZRZUEZAUFZWEGZWHBUGHZUHZRZWJUIHZWHGZCIZDIZWJJZWHGZDWHKZEIZWOBUJHZJZWHG
      ZWOWTXAJZWHGZLZEWKKZLZCWHKZWCWDWIWLWNXIUEVDWFCDEBWJXAWHWKWMWJMZXAMZWKMZWM
      MZUKULWGWHAUMZWKWDWCWHXNRWFAUNUOWGFIZWKRZFAKZXNWKRWCWFXQWDWCWFLZXPFAWCWFX
      OAGZXPWFXSLZWCXOWEGZXPAWEXOUPZBWJXOWKXJXLUQNOPURFAWKUSQUTWCWFWNWDXRWMXOGZ
      FAKWNXRYCFAWCWFXSYCXTWCYAYCYBBWJXOWMXJXMVANOPFWMAWJUIVESQURWCWFXIWDXRXHCW
      HWOWHGWOXOGZFAKZXRXHFWOACVBSXRYEXHXRYELZWSXGYFWRDWHWPWHGWPXOGZFAKZYFWRFWP
      ADVBSXRYEYHWRYEYHLYDYGLZFAKZXRWRYDYGFAVCXRYJWQXOGZFAKWRXRYIYKFAWCWFXSYIYK
      VFZXTWCYAYLYBWCYALZYIYKWOWPBWJXOXJVGTNOVHFWQAWOWPWJVISVJVKVLVMVNYFXFEWKXR
      WTWKGZYEXFXRYNLZYELZXCXEYPXBXOGZFAKZXCYOYEYRYOYDYQFAXRYNXSYDYQVFZWCYNWFXS
      YSXTWCYNLZYAYSYBWCYAYNYSYMYNLZYDYQYMYDYNYQWOWTBWJXAXOWKXJXKXLVOVPTVQNVROV
      HVSFXBAWTWOXAVISQYPXDXOGZFAKZXEYOYEUUCYOYDUUBFAXRYNXSYDUUBVFZWCYNWFXSUUDX
      TYTYAUUDYBWCYAYNUUDUUAYDUUBYMYDYNUUBWOWTBWJXAXOWKXJXKXLVTVPTVQNVROVHVSFXD
      AWOWTXAVISQWAVQPWATVMVNURWB $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d I i $.  $d J i $.
    $( The intersection of two ideals is an ideal.  (Contributed by Jeff
       Madsen, 16-Jun-2011.) $)
    inidl $p |- ( ( R e. RingOps /\ I e. ( Idl ` R ) /\ J e. ( Idl ` R ) )
                                          -> ( I i^i J ) e. ( Idl ` R ) ) $=
      ( crngo wcel cidl cfv w3a cpr cint cin intprg 3adant1 wa c0 wne wss prnzg
      wceq adantr prssg ibi jca intidl 3expb sylan2 3impb eqeltrrd ) ADEZBAFGZE
      ZCUJEZHBCIZJZBCKZUJUKULUNUOSUIBCUJUJLMUIUKULUNUJEZUKULNZUIUMOPZUMUJQZNUPU
      QURUSUKURULBCUJRTUQUSBCUJUJUJUAUBUCUIURUSUPUMAUDUEUFUGUH $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d R i k x y z $.  $d C i j k x y z $.
    $( The union of a nonempty chain of ideals is an ideal.  (Contributed by
       Jeff Madsen, 5-Jan-2011.) $)
    unichnidl $p |- ( ( R e. RingOps /\ ( C =/= (/) /\ C C_ ( Idl ` R ) /\
                              A. i e. C A. j e. C ( i C_ j \/ j C_ i ) ) )
                                          -> U. C e. ( Idl ` R ) ) $=
      ( vx vy vz vk wcel cfv wss cv wral wa eqid imp sylan2 wi ssel2 an32s cidl
      crngo c0 wne wo w3a cuni c1st crn cgi co c2nd wb isidl adantr dfss3 idlss
      ralimdv sylan2b unissb sylibr 3ad2antr2 wrex idl0cl r19.2z an12s 3adantr3
      ex eluni2 wceq sseq1 sseq2 orbi12d ralbidv rcla4v ad2antlr ancoms adantll
      ad2antrl idladdcl ancom2s expr an42s anasss simprl elunii syl2anc anassrs
      ad2antrr jaodan syldan exp32 rexlimdv ralrimiv 3adantr1 exp43 com23 imp41
      syl5bi idllmulcl sylanl2 simplrr idlrmulcl jca ralrimiva mpbir3and ) BUBI
      ZAUCUDZABUAJZKZCLZDLZKZXLXKKZUEZDAMZCAMZUFZNZAUGZXIIZXTBUHJZUIZKZYBUJJZXT
      IZELZFLZYBUKZXTIZFXTMZGLZYGBULJZUKZXTIZYGYLYMUKZXTIZNZGYCMZNZEXTMZXGYAYDY
      FUUAUFUMXREFGBYBYMXTYCYEYBOZYMOZYCOZYEOZUNUOXGXHXJYDXQXGXJNZXKYCKZCAMZYDX
      JXGXKXIIZCAMZUUHCAXIUPZXGUUJUUHXGUUIUUGCAXGUUIUUGBYBXKYCUUBUUDUQVHURPUSCA
      YCUTVAVBXGXHXJYFXQXGXHXJNNYEXKIZCAVCZYFXHXGXJUUMUUFXHUULCAMZUUMXJXGUUJUUN
      UUKXGUUJUUNXGUUIUULCAXGUUIUULBYBXKYEUUBUUEVDVHURPUSUULCAVEQVFCYEAVIVAVGXS
      YTEXTYGXTIYGHLZIZHAVCXSYTHYGAVIXSUUPYTHAXSUUOAIZUUPYTXSUUQUUPNZNYKYSXGUUR
      XRYKXGUURNZXJXQYKXHUUSXJXQYKUUSXJNZXQUUOXLKZXLUUOKZUEZDAMZYKUUTXQUVDUURXQ
      UVDRZXGXJUUQUVEUUPXPUVDCUUOAXKUUOVJZXOUVCDAUVFXMUVAXNUVBXKUUOXLVKXKUUOXLV
      LVMVNVOUOVPPUUTUVDNZYJFXTYHXTIYHXKIZCAVCUVGYJCYHAVIUVGUVHYJCAUVGXKAIZUVHY
      JUUTUVIUVHNZUVDYJUUTUVJNZUVDUUOXKKZXKUUOKZUEZYJUVKUVDUVNUVIUVDUVNRUUTUVHU
      VCUVNDXKAXLXKVJUVAUVLUVBUVMXLXKUUOVLXLXKUUOVKVMVOVSPUVKUVLYJUVMUVKUVLYJUU
      SXJUVJUVLYJRZXGXJUVJNZUURUVOXGUVPNZUURUVLYJUURUVLNUVQYGXKIZYJUUPUVLUVRUUQ
      UVLUUPUVRUUOXKYGSVQVRUVQUVRNYIXKIZUVIYJUVQUVRUVSXGXJUVJUVRUVSRZXGUVHXJUVI
      UVTXJUVINXGUVHNUUIUVTAXIXKSXGUUIUVHUVTXGUUINZUVHUVRUVSUWAUVRUVHUVSYGYHBYB
      XKUUBVTWAWBTQWCWDPUVPUVIXGUVRXJUVIUVHWEVPYIXKAWFWGQWBTWHPUUTUVJUVMYJUVJUV
      MNUUTYHUUOIZYJUVHUVMUWBUVIUVMUVHUWBXKUUOYHSVQVRUUTUWBNYIUUOIZUUQYJUUTUWBU
      WCXGXJUURUWBUWCRZXGUUPXJUUQUWDXJUUQNZXGUUPNZUUOXIIZUWDAXIUUOSZXGUWGUUPUWD
      XGUWGNUUPUWBUWCYGYHBYBUUOUUBVTWBTQWCTPUUSUUQXJUWBXGUUQUUPWEWIYIUUOAWFWGQW
      HWJWKTWLWMWSWNWKWDWOTXGUURXRYSUUSXHXJYSXQXGXJUURYSXGUUPXJUUQYSUWFUWENZYRG
      YCUWIYLYCIZNZYOYQUWKYNUUOIZUUQYOUWEUWFUWGUWJUWLUWHXGUUPUWGUWJUWLXGUWGUUPU
      WJUWLRXGUWGUUPUWJUWLYGYLBYBYMUUOYCUUBUUCUUDWTWPWQWRXAUWFXJUUQUWJXBZYNUUOA
      WFWGUWKYPUUOIZUUQYQUWEUWFUWGUWJUWNUWHXGUUPUWGUWJUWNXGUWGUUPUWJUWNRXGUWGUU
      PUWJUWNYGYLBYBYMUUOYCUUBUUCUUDXCWPWQWRXAUWMYPUUOAWFWGXDXEWCTVBTXDWLWMWSWN
      XF $.
      $( [5-Jan-2011] $)
  $}

  ${
    $d R x y z $.  $d S x y z $.  $d F x y z $.  $d Z x y z $.
    keridl.1 $e |- G = ( 1st ` S ) $.
    keridl.2 $e |- Z = ( Id ` G ) $.
    $( The kernel of a ring homomorphism is an ideal.  (Contributed by Jeff
       Madsen, 3-Jan-2011.) $)
    keridl $p |- ( ( R e. RingOps /\ S e. RingOps /\ F e. ( R RngHom S ) )
                                      -> ( `' F " { Z } ) e. ( Idl ` R ) ) $=
      ( vx vy wcel co cfv wa wb eqid elpreima 3syl wceq fvex elsnc vz crngo w3a
      crnghom ccnv csn cima cidl c1st crn wss wral c2nd isidl 3ad2ant1 rngohomf
      cgi cv wf cdm cnvimass fdm syl5sseq syl wfn ffn rngohom0 sylibr mpbir2and
      rngo0cl an4 rngohomadd adantr oveq12 adantl cgr rngogrpo grpoidcl grpolid
      3ad2ant2 ad2antrr 3eqtrd anbi12i 3imtr4g imdistanda rngogcl 3expib anim1d
      mpdan ex wi syld syl5bi anbi12d 3imtr4d ralrimiva anbi2i rngocl 3ad2antl1
      3expb anass1rs adantlrr rngohommul oveq2 ad2antlr rngohomcl rngorz syldan
      impl 3ad2antl2 adantlr anassrs oveq1 rngolz jca sylbid imp mpbir3and ) AU
      BJZBUBJZCABUDKJZUCZCUEEUFZUGZAUHLJZYDAUILZUJZUKZYFUQLZYDJZHURZIURZYFKZYDJ
      ZIYDULZUAURZYKAUMLZKZYDJZYKYPYQKZYDJZMZUAYGULZMZHYDULZXSXTYEYHYJUUEUCNYAH
      IUAAYFYQYDYGYIYFOZYQOZYGOZYIOZUNUOYBYGDUJZCUSZYHABCYFDYGUUJUUFUUHFUUJOZUP
      ZUUKCUTYDYGCYCVAYGUUJCVBVCVDYBYJYIYGJZYICLZYCJZYBUUKCYGVEZYJUUNUUPMNUUMYG
      UUJCVFZYGYIYCCPQXSXTUUNYAAYFYGYIUUFUUHUUIVJUOYBUUOERUUPABCYFDEYIUUFUUIFGV
      GUUOEYICSTVHVIYBUUDHYDYBYKYDJZMZYOUUCUUTYNIYDYBUUSYLYDJZYNYBYKYGJZYKCLZYC
      JZMZYLYGJZYLCLZYCJZMZMZYMYGJZYMCLZYCJZMZUUSUVAMYNUVJUVBUVFMZUVDUVHMZMZYBU
      VNUVBUVDUVFUVHVKYBUVQUVOUVMMUVNYBUVOUVPUVMYBUVOMZUVCERZUVGERZMZUVLERZUVPU
      VMUVRUWAUWBUVRUWAMUVLUVCUVGDKZEEDKZEUVRUVLUWCRUWAYKYLABCYFDYGUUFUUHFVLVMU
      WAUWCUWDRUVRUVCEUVGEDVNVOYBUWDERZUVOUWAXTXSUWEYAXTDVPJZUWEBDFVQUWFEUUJJUW
      EEDUUJUULGVREEDUUJUULGVSWIVDVTWAWBWJUVDUVSUVHUVTUVCEYKCSTZUVGEYLCSTWCUVLE
      YMCSTWDWEYBUVOUVKUVMXSXTUVOUVKWKYAXSUVBUVFUVKYKYLAYFYGUUFUUHWFWGUOWHWLWMY
      BUUSUVEUVAUVIYBUUKUUQUUSUVENUUMUURYGYKYCCPQZYBUUKUUQUVAUVINUUMUURYGYLYCCP
      QWNYBUUKUUQYNUVNNUUMUURYGYMYCCPQWOXIWPYBUUSUUCYBUUSUVEUUCUWHUVEUVBUVSMZYB
      UUCUVDUVSUVBUWGWQYBUWIUUCYBUWIMZUUBUAYGUWJYPYGJZMZYSUUAUWLYSYRYGJZYRCLZYC
      JZYBYSUWMUWOMNZUWIUWKYBUUKUUQUWPUUMUURYGYRYCCPQWAYBUVBUWKUWMUVSYBUWKUVBUW
      MXSXTUWKUVBMUWMYAXSUWKUVBUWMYPYKAYFYQYGUUFUUGUUHWRWTWSXAXBUWLUWNERUWOUWLU
      WNYPCLZUVCBUMLZKZUWQEUWRKZEYBUVBUWKUWNUWSRZUVSYBUWKUVBUXAYPYKABCYFYQUWRYG
      UUFUUHUUGUWROZXCXAXBUWIUWSUWTRZYBUWKUVSUXCUVBUVCEUWQUWRXDVOXEYBUWKUWTERZU
      WIYBUWKUWQUUJJZUXDYPABCYFDYGUUJUUFUUHFUULXFZXTXSUXEUXDYAUWQBDUWRUUJEGUULF
      UXBXGXJXHXKWBUWNEYRCSTVHVIUWLUUAYTYGJZYTCLZYCJZYBUUAUXGUXIMNZUWIUWKYBUUKU
      UQUXJUUMUURYGYTYCCPQWAYBUVBUWKUXGUVSYBUVBUWKUXGXSXTUVBUWKMUXGYAXSUVBUWKUX
      GYKYPAYFYQYGUUFUUGUUHWRWTWSXLXBUWLUXHERUXIUWLUXHUVCUWQUWRKZEUWQUWRKZEYBUV
      BUWKUXHUXKRZUVSYBUVBUWKUXMYKYPABCYFYQUWRYGUUFUUHUUGUXBXCXLXBUWIUXKUXLRZYB
      UWKUVSUXNUVBUVCEUWQUWRXMVOXEYBUWKUXLERZUWIYBUWKUXEUXOUXFXTXSUXEUXOYAUWQBD
      UWRUUJEGUULFUXBXNXJXHXKWBUXHEYTCSTVHVIXOWPWJWMXPXQXOWPXR $.
      $( [3-Jan-2011] $)
  $}

  ${
    $d R r s i x y a b $.  $d X i r s $.  $d H i r s $.
    pridlval.1 $e |- G = ( 1st ` R ) $.
    pridlval.2 $e |- H = ( 2nd ` R ) $.
    pridlval.3 $e |- X = ran G $.
    $( The class of prime ideals of a ring ` R ` .  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    pridlval $p |- ( R e. RingOps -> ( PrIdl ` R ) = { i e. ( Idl ` R ) |
                ( i =/= X /\ A. a e. ( Idl ` R ) A. b e. ( Idl ` R ) (
          A. x e. a A. y e. b ( x H y ) e. i -> ( a C_ i \/ b C_ i ) ) ) } ) $=
      ( vr cv c1st cfv c2nd wral cidl fveq2 vs crn wne co wcel wo wi crab crngo
      wss wa cpridl syl6eqr rneqd neeq2d oveqd eleq1d 2ralbidv imbi1d raleqbidv
      wceq anbi12d rabeqbidv df-pridl fvex rabex fvopab4 ) MUACDNZMNZOPZUBZUCZA
      NZBNZVIQPZUDZVHUEZBINZRAHNZRZVSVHUJVRVHUJUFZUGZIVISPZRZHWCRZUKZDWCUHVHGUC
      ZVMVNFUDZVHUEZBVRRAVSRZWAUGZICSPZRZHWLRZUKZDWLUHUIULVICVAZWFWODWCWLVICSTZ
      WPVLWGWEWNWPVKGVHWPVKEUBGWPVJEWPVJCOPEVICOTJUMUNLUMUOWPWDWMHWCWLWQWPWBWKI
      WCWLWQWPVTWJWAWPVQWIABVSVRWPVPWHVHWPVOFVMVNWPVOCQPFVICQTKUMUPUQURUSUTUTVB
      VCABDUAMHIVDWODWLCSVEVFVG $.
      $( [10-Jun-2010] $)

    $d P i x y a b $.
    $( The predicate "is a prime ideal".  (Contributed by Jeff Madsen,
       10-Jun-2010.) $)
    ispridl $p |- ( R e. RingOps -> ( P e. ( PrIdl ` R ) <-> ( P e. ( Idl ` R )
                /\ P =/= X /\ A. a e. ( Idl ` R ) A. b e. ( Idl ` R )
        ( A. x e. a A. y e. b ( x H y ) e. P -> ( a C_ P \/ b C_ P ) ) ) ) ) $=
      ( vi wcel cfv cv wne wral wss wa crngo cpridl co wo wi cidl crab pridlval
      w3a eleq2d wceq neeq1 eleq2 2ralbidv sseq2 orbi12d imbi12d anbi12d 3anass
      elrab bitr4i syl6bb ) DUANZCDUBOZNCMPZGQZAPBPFUCZVENZBIPZRAHPZRZVJVESZVIV
      ESZUDZUEZIDUFOZRHVPRZTZMVPUGZNZCVPNZCGQZVGCNZBVIRAVJRZVJCSZVICSZUDZUEZIVP
      RHVPRZUIZVCVDVSCABDMEFGHIJKLUHUJVTWAWBWITZTWJVRWKMCVPVECUKZVFWBVQWIVECGUL
      WLVOWHHIVPVPWLVKWDVNWGWLVHWCABVJVIVECVGUMUNWLVLWEVMWFVECVJUOVECVIUOUPUQUN
      URUTWAWBWIUSVAVB $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d R x y a b $.  $d P x y a b $.
    $( A prime ideal is an ideal.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    pridlidl $p |- ( ( R e. RingOps /\ P e. ( PrIdl ` R ) )
                                            -> P e. ( Idl ` R ) ) $=
      ( vx vy vb va crngo wcel cpridl cfv cidl c1st crn wne cv c2nd wral wss wa
      eqid co wo wi w3a ispridl 3anass syl6bb simprbda ) BGHZABIJHZABKJZHZABLJZ
      MZNZCODOBPJZUAAHDEOZQCFOZQURARUQARUBUCEUKQFUKQZSZUIUJULUOUSUDULUTSCDABUMU
      PUNFEUMTUPTUNTUEULUOUSUFUGUH $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y a b $.  $d P x y a b $.
    pridlnr.1 $e |- G = ( 1st ` R ) $.
    prdilnr.2 $e |- X = ran G $.
    $( A prime ideal is a proper ideal.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    pridlnr $p |- ( ( R e. RingOps /\ P e. ( PrIdl ` R ) ) -> P =/= X ) $=
      ( vx vy vb va crngo wcel cpridl cfv wne cv wral wss wa w3a cidl c2nd eqid
      co wo wi ispridl 3ancoma 3anass bitri syl6bb simprbda ) BKLZABMNLZADOZABU
      ANZLZGPHPBUBNZUDALHIPZQGJPZQUTARUSARUEUFIUPQJUPQZSZUMUNUQUOVATZUOVBSZGHAB
      CURDJIEURUCFUGVCUOUQVATVDUQUOVAUHUOUQVAUIUJUKUL $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R x y a b $.  $d P x y a b $.  $d A x a b $.  $d B x y a b $.
    $d H a b $.
    pridl.1 $e |- H = ( 2nd ` R ) $.
    $( The main property of a prime ideal.  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    pridl $p |- ( ( ( R e. RingOps /\ P e. ( PrIdl ` R ) ) /\
                        ( A e. ( Idl ` R ) /\ B e. ( Idl ` R ) /\
            A. x e. A A. y e. B ( x H y ) e. P ) ) -> ( A C_ P \/ B C_ P ) ) $=
      ( vb va wcel cfv wa cv wral wss wo wi eqid wceq crngo cpridl cidl co c1st
      crn wne ispridl df-3an syl6bb simplbda raleq sseq1 orbi1d imbi12d ralbidv
      w3a orbi2d rcla42v syl5com exp3a 3imp2 ) FUAKZEFUBLKZMZCFUCLZKZDVFKZANBNG
      UDEKZBDOZACOZCEPZDEPZQZVEVGVHVKVNRZVEVIBINZOZAJNZOZVREPZVPEPZQZRZIVFOJVFO
      ZVGVHMVOVCVDEVFKZEFUELZUFZUGZMZWDVCVDWEWHWDUQWIWDMABEFWFGWGJIWFSHWGSUHWEW
      HWDUIUJUKWCVOVQACOZVLWAQZRJICDVFVFVRCTZVSWJWBWKVQAVRCULWLVTVLWAVRCEUMUNUO
      VPDTZWJVKWKVNWMVQVJACVIBVPDULUPWMWAVMVLVPDEUMURUOUSUTVAVB $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R a b r s $.  $d P a b r s $.  $d X a b r s $.  $d H r s $.
    ispridl2.1 $e |- G = ( 1st ` R ) $.
    ispridl2.2 $e |- H = ( 2nd ` R ) $.
    ispridl2.3 $e |- X = ran G $.
    $( A condition that shows an ideal is prime.  For commutative rings, this
       is often taken to be the definition.  See ~ ispridlc for the equivalence
       in the commutative case.  (Contributed by Jeff Madsen, 19-Jun-2010.) $)
    ispridl2 $p |- ( ( R e. RingOps /\ ( P e. ( Idl ` R ) /\ P =/= X /\
          A. a e. X A. b e. X ( ( a H b ) e. P -> ( a e. P \/ b e. P ) ) ) )
                              -> P e. ( PrIdl ` R ) ) $=
      ( vs vr wcel cv wo wi wral wss wa syl crngo cidl cfv wne w3a cpridl idlss
      ssralv adantrr ralimdv adantrl syld adantlr r19.26-2 pm3.35 ralimi 2ralor
      co biimpi dfss3 orbi12i sylibr sylbir expcom syl6 ralrimdvva ex imdistand
      adantrd df-3an 3imtr4g ispridl sylibrd imp ) BUAMZABUBUCZMZAEUDZFNZGNZDUR
      AMZVSAMZVTAMZOZPZGEQZFEQZUEZABUFUCMZVOWHVQVRWAGKNZQFLNZQZWKARZWJARZOZPZKV
      PQLVPQZUEZWIVOVQVRSZWGSWSWQSWHWRVOWSWGWQVOVQWGWQPZVRVOVQWTVOVQSZWGWPLKVPV
      PXAWKVPMZWJVPMZSZSWGWEGWJQZFWKQZWPVOXDWGXFPVQVOXDSWGWFFWKQZXFVOXBWGXGPZXC
      VOXBSWKERXHBCWKEHJUGWFFWKEUHTUIVOXCXGXFPZXBVOXCSWJERZXIBCWJEHJUGXJWFXEFWK
      WEGWJEUHUJTUKULUMWLXFWOWLXFSWAWESZGWJQZFWKQZWOWAWEFGWKWJUNXMWDGWJQZFWKQZW
      OXLXNFWKXKWDGWJWAWDUOUPUPXOWBFWKQZWCGWJQZOZWOXOXRWBWCFGWKWJUQUSWMXPWNXQFW
      KAUTGWJAUTVAVBTVCVDVEVFVGVIVHVQVRWGVJVQVRWQVJVKFGABCDELKHIJVLVMVN $.
      $( [19-Jun-2010] $)
  $}

  ${
    $d R i j r s $.  $d X r s $.
    maxidlval.1 $e |- G = ( 1st ` R ) $.
    maxidlval.2 $e |- X = ran G $.
    $( The set of maximal ideals of a ring.  (Contributed by Jeff Madsen,
       5-Jan-2011.) $)
    maxidlval $p |- ( R e. RingOps -> ( MaxIdl ` R ) =
                              { i e. ( Idl ` R ) | ( i =/= X /\
                A. j e. ( Idl ` R ) ( i C_ j -> ( j = i \/ j = X ) ) ) } ) $=
      ( vr vs cv c1st cfv crn wne wceq wo wi cidl wral wa crngo cmaxidl syl6eqr
      wss fveq2 rneqd neeq2d eqeq2d orbi2d imbi2d raleqbidv rabeqbidv df-maxidl
      crab anbi12d fvex rabex fvopab4 ) HIABJZHJZKLZMZNZUSCJZUDZVDUSOZVDVBOZPZQ
      ZCUTRLZSZTZBVJUNUSENZVEVFVDEOZPZQZCARLZSZTZBVQUNUAUBUTAOZVLVSBVJVQUTARUEZ
      VTVCVMVKVRVTVBEUSVTVBDMEVTVADVTVAAKLDUTAKUEFUCUFGUCZUGVTVIVPCVJVQWAVTVHVO
      VEVTVGVNVFVTVBEVDWBUHUIUJUKUOULBCIHUMVSBVQARUPUQUR $.
      $( [5-Jan-2011] $)
  $}

  ${
    $d R i j $.  $d M i j $.  $d X i $.
    ismaxidl.1 $e |- G = ( 1st ` R ) $.
    ismaxidl.2 $e |- X = ran G $.
    $( The predicate "is a maximal ideal".  (Contributed by Jeff Madsen,
       5-Jan-2011.) $)
    ismaxidl $p |- ( R e. RingOps -> ( M e. ( MaxIdl ` R ) <->
                              ( M e. ( Idl ` R ) /\ M =/= X /\
                A. j e. ( Idl ` R ) ( M C_ j -> ( j = M \/ j = X ) ) ) ) ) $=
      ( vi crngo wcel cmaxidl cfv cv wne wss wceq wo wi wral wa cidl w3a eleq2d
      crab neeq1 sseq1 eqeq2 orbi1d imbi12d ralbidv anbi12d elrab 3anass bitr4i
      maxidlval syl6bb ) AIJZDAKLZJDHMZENZUSBMZOZVAUSPZVAEPZQZRZBAUALZSZTZHVGUD
      ZJZDVGJZDENZDVAOZVADPZVDQZRZBVGSZUBZUQURVJDAHBCEFGUOUCVKVLVMVRTZTVSVIVTHD
      VGUSDPZUTVMVHVRUSDEUEWAVFVQBVGWAVBVNVEVPUSDVAUFWAVCVOVDUSDVAUGUHUIUJUKULV
      LVMVRUMUNUP $.
      $( [5-Jan-2011] $)
  $}

  ${
    $d R j $.  $d M j $.
    $( A maximal ideal is an ideal.  (Contributed by Jeff Madsen,
       5-Jan-2011.) $)
    maxidlidl $p |- ( ( R e. RingOps /\ M e. ( MaxIdl ` R ) )
                                                -> M e. ( Idl ` R ) ) $=
      ( vj crngo wcel cmaxidl cfv cidl c1st crn wne cv wss wceq wo wi wral eqid
      wa w3a ismaxidl 3anass syl6bb simprbda ) ADEZBAFGEZBAHGZEZBAIGZJZKZBCLZMU
      LBNULUJNOPCUGQZSZUEUFUHUKUMTUHUNSACUIBUJUIRUJRUAUHUKUMUBUCUD $.
      $( [5-Jan-2011] $)

    $d R j $.  $d M j $.
    maxidlnr.1 $e |- G = ( 1st ` R ) $.
    maxidlnr.2 $e |- X = ran G $.
    $( A maximal ideal is proper.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    maxidlnr $p |- ( ( R e. RingOps /\ M e. ( MaxIdl ` R ) ) -> M =/= X ) $=
      ( vj crngo wcel cmaxidl cfv wa cidl wne cv wss wceq wo wi wral w3a biimpa
      ismaxidl simp2d ) AHIZCAJKIZLCAMKZIZCDNZCGOZPUJCQUJDQRSGUGTZUEUFUHUIUKUAA
      GBCDEFUCUBUD $.
      $( [16-Jun-2011] $)

    $d I j $.  $d X j $.
    $( A maximal ideal is a maximal proper ideal.  (Contributed by Jeff Madsen,
       16-Jun-2011.) $)
    maxidlmax $p |- ( ( ( R e. RingOps /\ M e. ( MaxIdl ` R ) ) /\
                ( I e. ( Idl ` R ) /\ M C_ I ) ) -> ( I = M \/ I = X ) ) $=
      ( vj crngo wcel cmaxidl cfv wa cidl wss wceq wo wi cv eqeq1 wral ismaxidl
      wne w3a biimpa simp3d sseq2 orbi12d imbi12d rcla4va sylan2 ancoms impr )
      AIJZDAKLJZMZCANLZJZDCOZCDPZCEPZQZURUPUSVBRZUPURDHSZOZVDDPZVDEPZQZRZHUQUAZ
      VCUPDUQJZDEUCZVJUNUOVKVLVJUDAHBDEFGUBUEUFVIVCHCUQVDCPZVEUSVHVBVDCDUGVMVFU
      TVGVAVDCDTVDCETUHUIUJUKULUM $.
      $( [16-Jun-2011] $)
  $}

  ${
    maxidln1.1 $e |- H = ( 2nd ` R ) $.
    maxidln1.2 $e |- U = ( Id ` H ) $.
    $( One is not contained in any maximal ideal.  (Contributed by Jeff Madsen,
       17-Jun-2011.) $)
    maxidln1 $p |- ( ( R e. RingOps /\ M e. ( MaxIdl ` R ) ) -> -. U e. M ) $=
      ( crngo wcel cmaxidl cfv wa wn c1st crn wne eqid maxidlnr cidl maxidlidl
      wb 1idl necon3bbid syldan mpbird ) AGHZDAIJHZKBDHZLZDAMJZNZOZAUIDUJUIPZUJ
      PZQUEUFDARJHZUHUKTADSUEUNKUGDUJABUICDUJULEUMFUAUBUCUD $.
      $( [17-Jun-2011] $)
  $}

  ${
    maxidln0.1 $e |- G = ( 1st ` R ) $.
    maxidln0.2 $e |- H = ( 2nd ` R ) $.
    maxidln0.3 $e |- Z = ( Id ` G ) $.
    maxidln0.4 $e |- U = ( Id ` H ) $.
    $( A ring with a maximal ideal is not the zero ring.  (Contributed by Jeff
       Madsen, 17-Jun-2011.) $)
    maxidln0 $p |- ( ( R e. RingOps /\ M e. ( MaxIdl ` R ) ) -> U =/= Z ) $=
      ( crngo wcel cmaxidl cfv wa wceq wn wne cidl maxidlidl maxidln1 syl2anc
      idl0cl syldan nelneq df-ne sylibr necomd ) AKLZEAMNLZOZFBUKFBPQZFBRUKFELZ
      BELQULUIUJEASNLUMAETACEFGIUCUDABDEHJUAFBEUEUBFBUFUGUH $.
      $( [17-Jun-2011] $)
  $}

