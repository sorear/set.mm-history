$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Banach Fixed Point Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d j k x y z D $.  $d j k x y z G $.  $d j k x y z J $.  $d j k w x y ph $.
    $d j k A $.  $d j k w x y z F $.  $d j k x y K $.  $d j k w x y z X $.
    bfp.1 $e |- X = dom dom D $.
    bfp.2 $e |- ( ph -> D e. CMet ) $.
    bfp.3 $e |- ( ph -> X =/= (/) ) $.
    bfp.4 $e |- ( ph -> K e. RR+ ) $.
    bfp.5 $e |- ( ph -> K < 1 ) $.
    bfp.6 $e |- ( ph -> F : X --> X ) $.
    bfp.7 $e |- ( ( ph /\ ( x e. X /\ y e. X ) ) ->
                  ( ( F ` x ) D ( F ` y ) ) <_ ( K x. ( x D y ) ) ) $.
    ${
      bfp.8 $e |- J = ( MetOpen ` D ) $.
      bfp.9 $e |- ( ph -> A e. X ) $.
      bfp.10 $e |- G = seq 1 ( ( F o. 1st ) , ( NN X. { A } ) ) $.
      $( Lemma for ~ bfp .  The sequence ` G ` , which simply starts from any
         point in the space and iterates ` F ` , satisfies the property that
         the distance from ` G ( n ) ` to ` G ( n + 1 ) ` decreases by at least
         ` K ` after each step.  Thus the total distance from any ` G ( i ) `
         to ` G ( j ) ` is bounded by a geometric series, and the sequence is
         Cauchy.  Therefore, it converges to a point ` ( ( ~~>t `` J ) `` G ) `
         since the space is complete. $)
      bfplem1 $p |- ( ph -> G ( ~~>t ` J ) ( ( ~~>t ` J ) ` G ) ) $=
        ( vk vj clm cfv cdm wcel wbr cms cca co cdiv cme cmsmet syl c1 cn cz 1z
        nnuz a1i algrf cr wf ffvelrn syl2anc metcl syl3anc rerpdivcl caddc cexp
        crp cv cmul cle wi wceq fveq2 oveq1 fveq2d oveq12d oveq2 oveq2d breq12d
        imbi2d leid algr0 1nn algrp1 mpan2 eqtrd cc rpre recnd exp1 cc0 divcan1
        wne rpne0 3brtr4d wa sylan peano2nn syl2an jca ralrimivva adantr oveq1d
        wral rcla42v sylc remulcl cn0 adantl nnnn0 reexpcl letr mpand clt rpgt0
        lemul1 syl112anc mulcom mulass expp1 eqtr4d bitrd sylan2 breq1d 3imtr4d
        wb expcom a2d nnind impcom geomcau cmscvg wfun cha methaus lmfun mpbid
        funfvbrb ) AGHUCUDZUEUFZGGUUCUDUUCUGZAEUHUFZGEUIUDUFUUDLADDFUDZEUJZIUKU
        JZIEUAGJKAUUFEULUFZLEUMUNZADGJFUOUPUSTUOUQUFAURUTZSPVAZAUUHVBUFZIVKUFZU
        UIVBUFZAUUJDJUFZUUGJUFZUUNUUKSAJJFVCZUUQUURPSJJDFVDVEDUUGEJKVFVGZNUUHIV
        HVEZNOUAVLZUPUFZAUVBGUDZUVBUOVIUJZGUDZEUJZUUIIUVBVJUJZVMUJZVNUGZAUBVLZG
        UDZUVKUOVIUJZGUDZEUJZUUIIUVKVJUJZVMUJZVNUGZVOAUOGUDZUOUOVIUJZGUDZEUJZUU
        IIUOVJUJZVMUJZVNUGZVOAUVJVOZAUVFUVEUOVIUJZGUDZEUJZUUIIUVEVJUJZVMUJZVNUG
        ZVOUWFUBUAUVBUVKUOVPZUVRUWEAUWMUVOUWBUVQUWDVNUWMUVLUVSUVNUWAEUVKUOGVQUW
        MUVMUVTGUVKUOUOVIVRVSVTUWMUVPUWCUUIVMUVKUOIVJWAWBWCWDUVKUVBVPZUVRUVJAUW
        NUVOUVGUVQUVIVNUWNUVLUVDUVNUVFEUVKUVBGVQUWNUVMUVEGUVKUVBUOVIVRVSVTUWNUV
        PUVHUUIVMUVKUVBIVJWAWBWCWDZUVKUVEVPZUVRUWLAUWPUVOUWIUVQUWKVNUWPUVLUVFUV
        NUWHEUVKUVEGVQUWPUVMUWGGUVKUVEUOVIVRVSVTUWPUVPUWJUUIVMUVKUVEIVJWAWBWCWD
        UWOAUUHUUHUWBUWDVNAUUNUUHUUHVNUGUUTUUHWEUNAUVSDUWAUUGEADGJFUOUPUSTUULSW
        FZAUWAUVSFUDZUUGAUOUPUFUWAUWRVPWGADGJFUOUOUPUSTUULSPWHWIAUVSDFUWQVSWJVT
        AUWDUUIIVMUJZUUHAUWCIUUIVMAIWKUFZUWCIVPAIAUUOIVBUFZNIWLUNZWMZIWNUNWBAUU
        HWKUFUWTIWOWQZUWSUUHVPAUUHUUTWMUXCAUUOUXDNIWRUNUUHIWPVGWJWSUVCAUVJUWLAU
        VCUVJUWLVOAUVCWTZIUVGVMUJZUWKVNUGZUVDFUDZUVFFUDZEUJZUWKVNUGZUVJUWLUXEUX
        JUXFVNUGZUXGUXKUXEUVDJUFZUVFJUFZWTBVLZFUDZCVLZFUDZEUJZIUXOUXQEUJZVMUJZV
        NUGZCJXHBJXHZUXLUXEUXMUXNAUPJGVCZUVCUXMUUMUPJUVBGVDXAZAUYDUVEUPUFZUXNUV
        CUUMUVBXBZUPJUVEGVDXCZXDAUYCUVCAUYBBCJJQXEXFUYBUXLUXHUXREUJZIUVDUXQEUJZ
        VMUJZVNUGBCUVDUVFJJUXOUVDVPZUXSUYIUYAUYKVNUYLUXPUXHUXREUXOUVDFVQXGUYLUX
        TUYJIVMUXOUVDUXQEVRWBWCUXQUVFVPZUYIUXJUYKUXFVNUYMUXRUXIUXHEUXQUVFFVQWBU
        YMUYJUVGIVMUXQUVFUVDEWAWBWCXIXJUXEUXJVBUFZUXFVBUFZUWKVBUFZUXLUXGWTUXKVO
        UXEUUJUXHJUFZUXIJUFZUYNAUUJUVCUUKXFZUXEUUSUXMUYQAUUSUVCPXFZUYEJJUVDFVDV
        EUXEUUSUXNUYRUYTUYHJJUVFFVDVEUXHUXIEJKVFVGUXEUXAUVGVBUFZUYOAUXAUVCUXBXF
        ZUXEUUJUXMUXNVUAUYSUYEUYHUVDUVFEJKVFVGZIUVGXKVEUXEUUPUWJVBUFZUYPAUUPUVC
        UVAXFZUXEUXAUVEXLUFZVUDVUBUXEUYFVUFUVCUYFAUYGXMUVEXNUNIUVEXOVEUUIUWJXKV
        EUXJUXFUWKXPVGXQUXEUVJUVGIVMUJZUVIIVMUJZVNUGZUXGUXEVUAUVIVBUFZUXAWOIXRU
        GZUVJVUIYJVUCUXEUUPUVHVBUFZVUJVUEAUXAUVBXLUFZVULUVCUXBUVBXNZIUVBXOXCZUU
        IUVHXKVEVUBAVUKUVCAUUOVUKNIXSUNXFUVGUVIIXTYAUXEVUGUXFVUHUWKVNUXEUVGWKUF
        UWTVUGUXFVPUXEUVGVUCWMAUWTUVCUXCXFZUVGIYBVEUXEVUHUUIUVHIVMUJZVMUJZUWKUX
        EUUIWKUFUVHWKUFUWTVUHVURVPUXEUUIVUEWMUXEUVHVUOWMVUPUUIUVHIYCVGUXEUWJVUQ
        UUIVMAUWTVUMUWJVUQVPUVCUXCVUNIUVBYDXCWBYEWCYFUXEUWIUXJUWKVNUXEUVFUXHUWH
        UXIEADGJFUVBUOUPUSTUULSPWHUVCAUYFUWHUXIVPUYGADGJFUVEUOUPUSTUULSPWHYGVTY
        HYIYKYLYMYNYOEGHRYPVEAUUCYQZUUDUUEYJAHYRUFZVUSAUUJVUTUUKEHRYSUNHYTUNGUU
        CUUBUNUUA $.
        $( [17-Jun-2014] $)

      $( Lemma for ~ bfp .  Using the point found in ~ bfplem1 , we show that
         this convergent point is a fixed point of ` F ` .  Since for any
         positive ` x ` , the sequence ` G ` is in ` B ( x / 2 , P ) ` for all
         ` k e. ( ZZ>= `` j ) ` (where ` P = ( ( ~~>t `` J ) `` G ) ` ), we
         have ` D ( G ( j + 1 ) , F ( P ) ) <_ D ( G ( j ) , P ) < x / 2 ` and
         ` D ( G ( j + 1 ) , P ) < x / 2 ` , so ` F ( P ) ` is in every
         neighborhood of ` P ` and ` P ` is a fixed point of ` F ` . $)
      bfplem2 $p |- ( ph -> E. z e. X ( F ` z ) = z ) $=
        ( vk clm cfv wcel wceq wrex cuni ctop cvv wbr cme cms cmsmet syl opntop
        vj cv fvex a1i bfplem1 eqid lmcl syl3anc uniopn2 eleqtrrd cc0 cle caddc
        co crp wral wa clt c2 cdiv cuz cn c1 adantr cz 1z eqidd rphalfcl adantl
        nnuz lmmcvg simpr ralimi nnz fveq2 oveq1d breq1d rcla4v peano2uz algrp1
        wi uzid adantlr sylibd cr ad2antrr wf algrf ffvelrn sylan metcl syl2anc
        jcad ad2antlr lt2halves readdcl mettri2 syl13anc remulcl jca ralrimivva
        rpre cmul oveq1 oveq2d breq12d rcla42v sylc 1re metge0 ltle sylancl mpd
        oveq2 lemul1a syl31anc cc recnd mulid2 breqtrd letrd wb mpbid 0re mpand
        leadd1 lelttr 3syld syl5 rexlimdva syl2an addid2 breqtrrd alrple mpbird
        ralrimiva letri3 mpbir2and meteq0 id eqeq12d rcla4ev ) AHIUCUDZUDZKUEZU
        UTGUDZUUTUFZDURZGUDZUVDUFZDKUGAUUTIUHZKAIUIUEZUUTUJUEZHUUTUUSUKZUUTUVGU
        EAFULUEZUVHAFUMUEUVKMFUNUOZFISUPUOUVIAHUUSUSZUTABCEFGHIJKLMNOPQRSTUAVAZ
        UUTHIUJUVGUVGVBVCVDAUVKKUVGUFUVLFIKLSVEUOVFZAUVBUUTFVJZVGUFZUVCAUVQUVPV
        GVHUKZVGUVPVHUKZAUVRUVPVGBURZVIVJZVHUKZBVKVLZAUWBBVKAUVTVKUEZVMZUVPUVTU
        WAVHUWEUVPUVTVNUKZUVPUVTVHUKZUWEUBURZHUDZKUEZUWIUUTFVJZUVTVOVPVJZVNUKZV
        MZUBUQURZVQUDZVLZUQVRUGUWFUWEUWIFUUTUWLUQUBHIVSUJKVRLSAUVKUWDUVLVTUVIUW
        EUVMUTWFVSWAUEZUWEWBUTUWEUWHVRUEVMUWIWCAUVJUWDUVNVTUWDUWLVKUEAUVTWDWEWG
        UWEUWQUWFUQVRUWQUWMUBUWPVLZUWEUWOVRUEZVMZUWFUWNUWMUBUWPUWJUWMWHWIUXAUWS
        UWOHUDZUUTFVJZUWLVNUKZUXBGUDZUUTFVJZUWLVNUKZVMZUXCUXFVIVJZUVTVNUKZUWFUX
        AUWSUXDUXGUXAUWOUWPUEZUWSUXDWQUXAUWOWAUEZUXKUWTUXLUWEUWOWJWEUWOWRUOZUWM
        UXDUBUWOUWPUWHUWOUFZUWKUXCUWLVNUXNUWIUXBUUTFUWHUWOHWKWLWMWNUOUXAUWSUWOV
        SVIVJZHUDZUUTFVJZUWLVNUKZUXGUXAUXOUWPUEZUWSUXRWQUXAUXKUXSUXMUWOUWOWOUOU
        WMUXRUBUXOUWPUWHUXOUFZUWKUXQUWLVNUXTUWIUXPUUTFUWHUXOHWKWLWMWNUOUXAUXQUX
        FUWLVNUXAUXPUXEUUTFAUWTUXPUXEUFUWDAEHKGUWOVSVRWFUAUWRAWBUTZTQWPWSWLWMWT
        XIUXAUXCXAUEZUXFXAUEZUVTXAUEZUXHUXJWQUXAUVKUXBKUEZUVAUYBAUVKUWDUWTUVLXB
        ZUWEVRKHXCZUWTUYEAUYGUWDAEHKGVSVRWFUAUYATQXDVTVRKUWOHXEXFZAUVAUWDUWTUVO
        XBZUXBUUTFKLXGVDZUXAUVKUXEKUEZUVAUYCUYFUXAKKGXCZUYEUYKAUYLUWDUWTQXBZUYH
        KKUXBGXEXHZUYIUXEUUTFKLXGVDZUWDUYDAUWTUVTXRZXJZUXCUXFUVTXKVDUXAUVPUXIVH
        UKZUXJUWFUXAUVPUXEUVBFVJZUXFVIVJZUXIAUVPXAUEZUWDUWTAUVKUVBKUEZUVAVUAUVL
        AUYLUVAVUBQUVOKKUUTGXEZXHZUVOUVBUUTFKLXGVDZXBZUXAUYSXAUEZUYCUYTXAUEUXAU
        VKUYKVUBVUGUYFUYNUXAUYLUVAVUBUYMUYIVUCXHZUXEUVBFKLXGVDZUYOUYSUXFXLXHUXA
        UYBUYCUXIXAUEZUYJUYOUXCUXFXLXHZUXAUVKUYKVUBUVAUVPUYTVHUKUYFUYNVUHUYIUVB
        UUTUXEFKLXMXNUXAUYSUXCVHUKZUYTUXIVHUKZUXAUYSJUXCXSVJZUXCVUIUXAJXAUEZUYB
        VUNXAUEAVUOUWDUWTAJVKUEVUOOJXRUOZXBZUYJJUXCXOXHUYJUXAUYEUVAVMUVTGUDZCUR
        ZGUDZFVJZJUVTVUSFVJZXSVJZVHUKZCKVLBKVLZUYSVUNVHUKZUXAUYEUVAUYHUYIXPAVVE
        UWDUWTAVVDBCKKRXQXBVVDVVFUXEVUTFVJZJUXBVUSFVJZXSVJZVHUKBCUXBUUTKKUVTUXB
        UFZVVAVVGVVCVVIVHVVJVURUXEVUTFUVTUXBGWKWLVVJVVBVVHJXSUVTUXBVUSFXTYAYBVU
        SUUTUFZVVGUYSVVIVUNVHVVKVUTUVBUXEFVUSUUTGWKYAVVKVVHUXCJXSVUSUUTUXBFYJYA
        YBYCYDUXAVUNVSUXCXSVJZUXCVHUXAVUOVSXAUEZUYBVGUXCVHUKZVMJVSVHUKZVUNVVLVH
        UKVUQVVMUXAYEUTUXAUYBVVNUYJUXAUVKUYEUVAVVNUYFUYHUYIUXBUUTFKLYFVDXPAVVOU
        WDUWTAJVSVNUKZVVOPAVUOVVMVVPVVOWQVUPYEJVSYGYHYIXBJVSUXCYKYLUXAUXCYMUEVV
        LUXCUFUXAUXCUYJYNUXCYOUOYPYQUXAVUGUYBUYCVULVUMYRVUIUYJUYOUYSUXCUXFUUBVD
        YSYQUXAVUAVUJUYDUYRUXJVMUWFWQVUFVUKUYQUVPUXIUVTUUCVDUUAUUDUUEUUFYIAVUAU
        YDUWFUWGWQUWDVUEUYPUVPUVTYGUUGYIUWEUVTYMUEUWAUVTUFUWEUVTUWDUYDAUYPWEYNU
        VTUUHUOUUIUULAVUAVGXAUEZUVRUWCYRVUEYTBUVPVGUUJYHUUKAUVKVUBUVAUVSUVLVUDU
        VOUVBUUTFKLYFVDAVUAVVQUVQUVRUVSVMYRVUEYTUVPVGUUMYHUUNAUVKVUBUVAUVQUVCYR
        UVLVUDUVOUVBUUTFKLUUOVDYSUVFUVCDUUTKUVDUUTUFZUVEUVBUVDUUTUVDUUTGWKVVRUU
        PUUQUURXH $.
        $( [5-Jun-2014] $)
    $}

    $( Banach fixed point theorem, also known as contraction mapping theorem.
       A contraction on a complete metric space has a unique fixed point.  We
       show existence in the lemmas, and uniqueness here - if ` F ` has two
       fixed points, then the distance between them is less than ` K ` times
       itself, a contradiction.  (Contributed by Jeff Madsen, 2-Sep-2009.)
       (The proof was shortened by Mario Carneiro, 5-Jun-2014.) $)
    bfp $p |- ( ph -> E! z e. X ( F ` z ) = z ) $=
      ( wceq wcel c1 co cc0 vw cv cfv wrex wa wi wral wreu wex c0 n0 sylib c1st
      wne ccom cn csn cxp cseq copn cms adantr crp clt wbr wf cmul adantlr eqid
      cle simpr bfplem2 ex exlimdv mpd cmin oveq12 adantl eqbrtrrd cr wb cmsmet
      cme syl ad2antrr simplrl simplrr metcl syl3anc rpre remulcl suble0 mpbird
      syl2anc ax-1cn a1i recnd subdir mulid2 oveq1d eqtrd resubcl sylancr mul01
      cc 1re 3brtr4d 0re posdif sylancl mpbid lemul2 syl112anc metge0 mpbir2and
      letri3 meteq0 ralrimivva fveq2 id eqeq12d equequ1 imbi12d ralbidv cbvralv
      anbi1d reu4 sylanbrc ) ADUBZFUCZYIPZDHUDZYKCUBZFUCZYMPZUEZYIYMPZUFZCHUGZD
      HUGZYKDHUHAUAUBZHQZUAUIZYLAHUJUNZUUCKUAHUKULAUUBYLUAAUUBYLAUUBUEBCDUUAEFF
      UMUOUPUUAUQURRUSZEUTUCZGHIAEVAQZUUBJVBAUUDUUBKVBAGVCQZUUBLVBAGRVDVEZUUBMV
      BAHHFVFUUBNVBABUBZHQZYMHQZUEZUUJFUCZYNESZGUUJYMESZVGSZVJVEZUUBOVHUUFVIAUU
      BVKUUEVIVLVMVNVOAUUNUUJPZYOUEZUUJYMPZUFZCHUGZBHUGYTAUVBBCHHAUUMUEZUUTUVAU
      VDUUTUEZUUPTPZUVAUVEUVFUUPTVJVEZTUUPVJVEZUVEUVGRGVPSZUUPVGSZUVITVGSZVJVEZ
      UVEUUPUUQVPSZTUVJUVKVJUVEUVMTVJVEZUUPUUQVJVEZUVEUUOUUPUUQVJUUTUUOUUPPUVDU
      UNUUJYNYMEVQVRUVDUURUUTOVBVSUVEUUPVTQZUUQVTQZUVNUVOWAUVEEWCQZUUKUULUVPAUV
      RUUMUUTAUUGUVRJEWBWDWEZAUUKUULUUTWFZAUUKUULUUTWGZUUJYMEHIWHWIZUVEGVTQZUVP
      UVQAUWCUUMUUTAUUHUWCLGWJWDZWEZUWBGUUPWKWNUUPUUQWLWNWMUVEUVJRUUPVGSZUUQVPS
      ZUVMUVERXEQZGXEQUUPXEQZUVJUWGPUWHUVEWOWPUVEGUWEWQUVEUUPUWBWQZRGUUPWRWIUVE
      UWFUUPUUQVPUVEUWIUWFUUPPUWJUUPWSWDWTXAUVEUVIXEQUVKTPUVEUVIAUVIVTQZUUMUUTA
      RVTQZUWCUWKXFUWDRGXBXCWEZWQUVIXDWDXGUVEUVPTVTQZUWKTUVIVDVEZUVGUVLWAUWBUWN
      UVEXHWPUWMAUWOUUMUUTAUUIUWOMAUWCUWLUUIUWOWAUWDXFGRXIXJXKWEUUPTUVIXLXMWMUV
      EUVRUUKUULUVHUVSUVTUWAUUJYMEHIXNWIUVEUVPUWNUVFUVGUVHUEWAUWBXHUUPTXPXJXOUV
      EUVRUUKUULUVFUVAWAUVSUVTUWAUUJYMEHIXQWIXKVMXRUVCYSBDHUUJYIPZUVBYRCHUWPUUT
      YPUVAYQUWPUUSYKYOUWPUUNYJUUJYIUUJYIFXSUWPXTYAYFBDCYBYCYDYEULYKYODCHYQYJYN
      YIYMYIYMFXSYQXTYAYGYH $.
      $( [5-Jun-2014] $) $( [2-Sep-2009] $)
  $}

