$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Basic topological facts
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    ntrcmp.1 $e |- X = U. J $.
    $( An interior of a complement is the complement of the closure.  This set
       is also known as the exterior of ` A ` .  (Contributed by Jeff Hankins,
       31-Aug-2009.) $)
    ntrcmp $p |- ( ( J e. Top /\ A C_ X ) -> ( ( int ` J ) ` ( X \ A ) ) = ( X
    \ ( ( cls ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa cdif cnt cfv ccl wceq difss ntrval2 mpan2 adantr simpr
      dfss4 sylib fveq2d difeq2d eqtrd ) BEFZACGZHZCAIZBJKKZCCUGIZBLKZKZIZCAUJK
      ZIUDUHULMZUEUDUGCGUNCANUGBCDOPQUFUKUMCUFUIAUJUFUEUIAMUDUERACSTUAUBUC $.
      $( [31-Aug-2009] $)
  $}

  ${
    clscmp.1 $e |- X = U. J $.
    $( A closure of a complement is the complement of the interior.
       (Contributed by Jeff Hankins, 31-Aug-2009.) $)
    clscmp $p |- ( ( J e. Top /\ A C_ X ) -> ( ( cls ` J ) ` ( X \ A ) ) = ( X
    \ ( ( int ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa cdif ccl cfv cnt wceq difss clsval2 mpan2 adantr simpr
      dfss4 sylib fveq2d difeq2d eqtrd ) BEFZACGZHZCAIZBJKKZCCUGIZBLKZKZIZCAUJK
      ZIUDUHULMZUEUDUGCGUNCANUGBCDOPQUFUKUMCUFUIAUJUFUEUIAMUDUERACSTUAUBUC $.
      $( [31-Aug-2009] $)
  $}

  ${
    topbnd.1 $e |- X = U. J $.
    $( Two equivalent expressions for the boundary of a topology.  (Contributed
       by Jeff Hankins, 23-Sep-2009.) $)
    topbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( cls ` J ) ` A ) i^i ( ( cls
    ` J ) ` ( X \ A ) ) ) = ( ( ( cls ` J ) ` A ) \ ( ( int ` J ) ` A ) ) ) $=
      ( ctop wcel wss wa ccl cfv cdif cin cnt clscmp ineq2d indif2 syl6eq df-ss
      wceq clsss3 sylib difeq1d eqtrd ) BEFACGHZABIJZJZCAKUEJZLZUFCLZABMJJZKZUF
      UJKUDUHUFCUJKZLUKUDUGULUFABCDNOUFCUJPQUDUIUFUJUDUFCGUIUFSABCDTUFCRUAUBUC
      $.
      $( [23-Sep-2009] $)
  $}

  ${
    opnbnd.1 $e |- X = U. J $.
    $( A set is open iff it is disjoint from its boundary.  (Contributed by
       Jeff Hankins, 23-Sep-2009.) $)
    opnbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( A e. J <-> ( A i^i ( ( ( cls ` J
    ) ` A ) i^i ( ( cls ` J ) ` ( X \ A ) ) ) ) = (/) ) ) $=
      ( ctop wcel wss wa cnt cfv wceq ccl cdif cin disjdif a1i eqeq1d syl5ibcom
      c0 ineq1 ntrss2 adantr inssdif0 sscls df-ss sylib eqcomd eqimss syl sylan
      sstr sylan2br eqssd ex impbid isopn3 topbnd ineq2d 3bitr4d ) BEFACGHZABIJ
      JZAKZAABLJZJZVAMZNZSKZABFAVDCAMVCJNZNZSKUTVBVGUTVAVENZSKZVBVGVKUTVAVDOPVB
      VJVFSVAAVETQRUTVGVBUTVGHVAAUTVAAGVGABCDUAUBVGUTAVDNZVAGZAVAGZAVDVAUCUTAVL
      GZVMVNUTAVLKVOUTVLAUTAVDGVLAKABCDUDAVDUEUFUGAVLUHUIAVLVAUKUJULUMUNUOABCDU
      PUTVIVFSUTVHVEAABCDUQURQUS $.
      $( [23-Sep-2009] $)

    $( A set is closed iff it contains its boundary.  (Contributed by Jeff
       Hankins, 1-Oct-2009.) $)
    cldbnd $p |- ( ( J e. Top /\ A C_ X ) -> ( A e. ( Clsd ` J ) <-> ( ( ( cls
    ` J ) ` A ) i^i ( ( cls ` J ) ` ( X \ A ) ) ) C_ A ) ) $=
      ( ctop wcel wss wa cfv cdif cin c0 adantl incom ex sylbi ineq2d wb adantr
      wceq ccld ccl iscld3 eqimss syl6bi ssinss1 syl6 sslin disjdif eqtri sseq0
      sylancl dfss4 fveq2 eqcomd syl5eq eqeq1d difss opnbnd mpan2 bitr4d opncld
      wi eleq1 sylibd sylbid syld impbid ) BEFZACGZHZABUAIZFZABUBIZIZCAJZVNIZKZ
      AGZVKVMVOAGZVSVKVMVOATVTABCDUCVOAUDUEVOVQAUFUGVKVSVPVRKZLTZVMVKVSWBVKVSHW
      AVPAKZGZWCLTWBVSWDVKVRAVPUHMWCAVPKLVPANACUIUJWAWCUKULOVKWBVPBFZVMVKWBVPVQ
      CVPJZVNIZKZKZLTZWEVKWAWILVKVRWHVPVKVRVQVOKWHVOVQNVKVOWGVQVJVOWGTZVIVJWFAT
      ZWKACUMZWLWGVOWFAVNUNUOPMQUPQUQVIWEWJRZVJVIVPCGWNCAURVPBCDUSUTSVAVKWEWFVL
      FZVMVIWEWOVCVJVIWEWOVPBCDVBOSVJWOVMRZVIVJWLWPWMWFAVLVDPMVEVFVGVH $.
      $( [1-Oct-2009] $)
  $}

  ${
    $d o J $.  $d o O $.  $d o X $.
    ntruni.1 $e |- X = U. J $.
    $( A union of interiors is a subset of the interior of the union.  The
       reverse inclusion may not hold.  (Contributed by Jeff Hankins,
       31-Aug-2009.) $)
    ntruni $p |- ( ( J e. Top /\ O C_ ~P X ) -> U_ o e. O ( ( int ` J ) ` o
    ) C_ ( ( int ` J ) ` U. O ) ) $=
      ( ctop wcel cpw wss wa cv cnt cfv cuni wral ciun elssuni wi sspwuni ntrss
      3expia sylan2b syl5 ralrimiv iunss sylibr ) BFGZCDHIZJZAKZBLMZMZCNZUKMZIZ
      ACOACULPUNIUIUOACUJCGUJUMIZUIUOUJCQUHUGUMDIZUPUORCDSUGUQUPUOUMUJBDETUAUBU
      CUDACULUNUEUF $.
      $( [31-Aug-2009] $)
  $}

  ${
    clsun.1 $e |- X = U. J $.
    $( A pairwise union of closures is the closure of the union.  (Contributed
       by Jeff Hankins, 31-Aug-2009.) $)
    clsun $p |- ( ( J e. Top /\ A C_ X /\ B C_ X ) -> ( ( cls ` J ) ` ( A u. B
    ) ) = ( ( ( cls ` J ) ` A ) u. ( ( cls ` J ) ` B ) ) ) $=
      ( wcel wss cun cfv cdif difundi wceq difss wa unss ntrcmp syl2anc 3adant3
      cin 3adant2 w3a ccl cnt fveq2i ntrin mp3an23 3ad2ant1 syl5eq simp1 biimpi
      ctop 3adant1 ineq12d syl6eqr 3eqtr3d ccld clscld cldss dfss4 sylib clsss3
      difeq2d jca bitri ) CUKFZADGZBDGZUAZDDABHZCUBIZIZJZJZDDAVJIZBVJIZHZJZJZVK
      VPVHVLVQDVHDVIJZCUCIZIZDAJZVTIZDBJZVTIZSZVLVQVHWAWBWDSZVTIZWFVSWGVTDABKUD
      VEVFWHWFLZVGVEWBDGWDDGWIDAMDBMWBWDCDEUEUFUGUHVHVEVIDGZWAVLLVEVFVGUIZVFVGW
      JVEVFVGNWJABDOUJULZVICDEPQVHWFDVNJZDVOJZSVQVHWCWMWEWNVEVFWCWMLVGACDEPRVEV
      GWEWNLVFBCDEPTUMDVNVOKUNUOVBVHVKDGZVMVKLVHVEVKCUPIFZWOWKVHVEWJWPWKWLVICDE
      UQQVKCDEURQVKDUSUTVHVNDGZVODGZNZVRVPLZVHWQWRVEVFWQVGACDEVARVEVGWRVFBCDEVA
      TVCWSVPDGWTVNVODOVPDUSVDUTUO $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d c C $.  $d c J $.  $d c X $.
    clsint2.1 $e |- X = U. J $.
    $( The closure of an intersection is a subset of the intersection of the
       closures.  (Contributed by Jeff Hankins, 31-Aug-2009.) $)
    clsint2 $p |- ( ( J e. Top /\ C C_ ~P X ) -> ( ( cls ` J ) ` |^| C ) C_
    |^|_ c e. C ( ( cls ` J ) ` c ) ) $=
      ( ctop wcel cpw wss wa cint ccl cfv cv wral ciin cuni wi sspwuni elssuni
      sstr2 syl adantl intss1 clsss syl3an3 3com23 3expia syld impancom sylan2b
      ralrimiv ssiin sylibr ) BFGZACHIZJZAKZBLMZMZDNZUSMZIZDAOUTDAVBPIUQVCDAUPU
      OAQZCIZVAAGZVCRACSUOVFVEVCUOVFJVEVACIZVCVFVEVGRZUOVFVAVDIVHVAATVAVDCUAUBU
      CUOVFVGVCUOVGVFVCVFUOVGURVAIVCVAAUDVAURBCEUEUFUGUHUIUJUKULDAVBUTUMUN $.
      $( [31-Aug-2009] $)
  $}

  ${
    $d c o A $.  $d c o J $.  $d c o X $.
    opnregcld.1 $e |- X = U. J $.
    $( A set is regularly closed iff it is the closure of some open set.
       (Contributed by Jeff Hankins, 27-Sep-2009.) $)
    opnregcld $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( cls ` J ) ` ( ( int ` J )
    ` A ) ) = A <-> E. o e. J A = ( ( cls ` J ) ` o ) ) ) $=
      ( ctop wcel wss wa cnt cfv wceq cv wrex ntropn fveq2 syldan clsss syl3anc
      ccl eqcom biimpi eqeq2d rcla4ev syl2an simpl eltopss clsss3 ntrss2 clsidm
      ex sseqtrd ntrss3 simpr sscls ssntr syl22anc eqssd adantlr fveq2d eqeq12d
      id syl5ibrcom rexlimdva impbid ) CFGZADHZIZACJKZKZCTKZKZALZABMZVKKZLZBCNZ
      VHVMVQVHVJCGAVLLZVQVMACDEOVMVRVLAUAUBVPVRBVJCVNVJLVOVLAVNVJVKPUCUDUEUKVHV
      PVMBCVHVNCGZIVMVPVOVIKZVKKZVOLZVFVSWBVGVFVSIZWAVOWCWAVOVKKZVOWCVFVODHZVTV
      OHZWAWDHVFVSUFZVFVSVNDHZWEVNCDEUGZVNCDEUHQZVFVSWEWFWJVOCDEUIQVOVTCDERSVFV
      SWHWDVOLWIVNCDEUJQULWCVFVTDHZVNVTHZVOWAHWGVFVSWEWKWJVOCDEUMQWCVFWEVSVNVOH
      ZWLWGWJVFVSUNVFVSWHWMWIVNCDEUOQVOCVNDEUPUQVTVNCDERSURUSVPVLWAAVOVPVJVTVKA
      VOVIPUTVPVBVAVCVDVE $.
      $( [27-Sep-2009] $)

    $( A set if regularly open iff it is the interior of some closed set.
       (Contributed by Jeff Hankins, 27-Sep-2009.) $)
    cldregopn $p |- ( ( J e. Top /\ A C_ X ) -> ( ( ( int ` J ) ` ( ( cls ` J )
    ` A ) ) = A <-> E. c e. ( Clsd ` J ) A = ( ( int ` J ) ` c ) ) ) $=
      ( ctop wcel wss wa ccl cfv cnt wceq ccld wrex fveq2 syldan ntrss syl3anc
      cv clscld eqcom biimpi eqeq2d rcla4ev syl2an ex simpl cldss ntrss2 clsss2
      mpd3an3 ntridm ntrss3 clsss3 sscls eqsstr3d adantlr id eqeq12d syl5ibrcom
      eqssd fveq2d rexlimdva impbid ) BFGZACHZIZABJKZKZBLKZKZAMZADTZVKKZMZDBNKZ
      OZVHVMVRVHVJVQGAVLMZVRVMABCEUAVMVSVLAUBUCVPVSDVJVQVNVJMVOVLAVNVJVKPUDUEUF
      UGVHVPVMDVQVHVNVQGZIVMVPVOVIKZVKKZVOMZVFVTWCVGVFVTIZWBVOWDVFVNCHZWAVNHZWB
      VOHVFVTUHZVNBCEUIZVFVTVOVNHZWFVFVTWEWIWHVNBCEUJQVNVOBCEUKULVNWABCERSWDVOV
      OVKKZWBVFVTWEWJVOMWHVNBCEUMQWDVFWACHZVOWAHZWJWBHWGVFVTVOCHZWKVFVTWEWMWHVN
      BCEUNQZVOBCEUOQVFVTWMWLWNVOBCEUPQWAVOBCERSUQVBURVPVLWBAVOVPVJWAVKAVOVIPVC
      VPUSUTVAVDVE $.
      $( [27-Sep-2009] $)
  $}

  $( Two neighborhoods intersect to form a neighborhood of the intersection.
     (Contributed by Jeff Hankins, 31-Aug-2009.) $)
  neiin $p |- ( ( J e. Top /\ M e. ( ( nei ` J ) ` A ) /\ N e. ( ( nei ` J ) `
  B ) ) -> ( M i^i N ) e. ( ( nei ` J ) ` ( A i^i B ) ) ) $=
    ( cfv cin wss wa simpr wb simpl neiss2 neii1 neiint syl3anc ssinss1 3adant3
    wcel syl ctop cnei w3a cuni eqid mpbid inss2 3adant2 syl5ss ssin sylib wceq
    cnt jca simp1 ntrin sseqtr4d mpbird ) CUASZDACUBFZFSZEBUTFSZUCZDEGZABGZUTFS
    ZVEVDCUMFZFZHZVCVEDVGFZEVGFZGZVHVCVEVJHZVEVKHZIVEVLHVCVMVNUSVAVMVBUSVAIZAVJ
    HZVMVOVAVPUSVAJVOUSACUDZHZDVQHZVAVPKUSVALZACDVQVQUEZMZACDVQWANZACDVQWAOPUFA
    BVJQTRVCVEBVKABUGUSVBBVKHZVAUSVBIZVBWDUSVBJWEUSBVQHEVQHZVBWDKUSVBLBCEVQWAMB
    CEVQWANZBCEVQWAOPUFUHUIUNVEVJVKUJUKVCUSVSWFVHVLULUSVAVBUOUSVAVSVBWCRUSVBWFV
    AWGUHDECVQWAUPPUQUSVAVFVIKZVBVOUSVEVQHZVDVQHZWHVTVOVRWIWBABVQQTVOVSWJWCDEVQ
    QTVECVDVQWAOPRUR $.
    $( [31-Aug-2009] $)

  ${
    $d c o x y F $.  $d c o x y J $.  $d c o x y K $.  $d c o x y X $.
    $d c o x y Y $.
    cncls.1 $e |- X = U. J $.
    cncls.2 $e |- Y = U. K $.
    $( Continuity in terms of closure.  (Contributed by Jeff Hankins,
       1-Oct-2009.) $)
    cncls $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y ) -> ( F e. ( J Cn K )
    <-> A. x ( x C_ X -> ( F " ( ( cls ` J ) ` x ) ) C_ ( ( cls ` K ) ` ( F " x
    ) ) ) ) ) $=
      ( vc wcel cima cfv wa wss wb 3ad2ant3 ad2antrr adantr syl2anc cvv ctop wf
      w3a ccn co ccnv cv ccld wral ccl wi iscncl 3adant3 ibar wfun ffun simpll1
      wal simpl2 crn imassrn frn syl5ss clscld imaeq2 eleq1d rcla4v syl simpll2
      wceq imp sscls cdm sseq2 biimpar sylan 3ad2antl3 funimass3 clsss2 syl3anc
      fdm mpbid funimass2 exp31 com23 alrimdv wfn fndm cnvimass mpbii 3syl cuni
      ffn uniexg syl5eqel adantl simpl ssexg 3adant2 sseq1 fveq2 imaeq2d fveq2d
      sseq12d imbi12d cla4gv mpid cldss 3ad2antl2 sylancl clsss simpr a1i sstrd
      ssid syl5sseq clsss3 sseqtr4d iscld4 mpbird ralrimdv impbid 3bitr2d ) CUA
      JZDUAJZEFBUBZUCZBCDUDUEJZYFBUFZIUGZKZCUHLZJZIDUHLZUIZMZYOAUGZENZBYQCUJLZL
      ZKZBYQKZDUJLZLZNZUKZAURZYDYEYHYPOYFIBCDEFGHULUMYFYDYOYPOYEYFYOUNPYGYOUUGY
      GYOUUFAYGYRYOUUEYGYRYOUUEYGYRMZYOMZBUOZYTYIUUDKZNZUUEYGUUJYRYOYFYDUUJYEEF
      BUPPZQZUUIYDUUKYLJZYQUUKNZUULYDYEYFYRYOUQUUHYOUUOUUHUUDYNJZYOUUOUKUUHYEUU
      BFNZUUQYDYEYFYRUSYGUURYRYFYDUURYEYFUUBBUTFBYQVAEFBVBVCPZRUUBDFHVDSYMUUOIU
      UDYNYJUUDVJYKUUKYLYJUUDYIVEVFVGVHVKUUIUUBUUDNZUUPUUIYEUURUUTYDYEYFYRYOVIY
      GUURYRYOUUSQUUBDFHVLSUUIUUJYQBVMZNZUUTUUPOUUNUUHUVBYOYFYDYRUVBYEYFUVAEVJZ
      YRUVBEFBWAZUVCUVBYRUVAEYQVNVOVPVQRYQUUDBVRSWBUUKYQCEGVSVTYTUUDBWCSWDWEWFY
      GUUGYMIYNYGYJYNJZUUGYMYGUVEUUGYMYGUVEMZUUGMZYMYKYSLZYKNZUVGBUVHKZYJNZUVIU
      VGUVJBYKKZUUCLZYJUVFUUGUVJUVMNZUVFUUGYKENZUVNYGUVOUVEYFYDUVOYEYFBEWGZUVCU
      VOEFBWMZEBWHZUVCYKUVANZUVOBYJWIZUVAEYKVNZWJWKPZRUVFYKTJZUUGUVOUVNUKZUKYGU
      WCUVEYDYFUWCYEYDETJZYFUWCYDECWLTGCUAWNWOUWEYFMZUVOUWEUWCUWFUVSUVOUVTYFUVS
      UVOOZUWEYFUVPUVCUWGUVQUVRUWAWKWPWJUWEYFWQYKETWRSVPWSRUUFUWDAYKTYQYKVJZYRU
      VOUUEUVNYQYKEWTUWHUUAUVJUUDUVMUWHYTUVHBYQYKYSXAXBUWHUUBUVLUUCYQYKBVEXCXDX
      EXFVHXGVKUVFUVMYJNUUGUVFUVMYJUUCLZYJUVFYEYJFNZUVLYJNZUVMUWINYDYEYFUVEUSZY
      EYDUVEUWJYFYJDFHXHXIUVFUUJYKYKNUWKYGUUJUVEUUMRYKXOYKYJBWCXJYJUVLDFHXKVTUV
      FYEUVEYJYJNZUWIYJNUWLYGUVEXLUWMUVFYJXOXMYJYJDFHVSVTXNRXNUVGUUJUVHUVANUVKU
      VIOYGUUJUVEUUGUUMQUVGUVHEUVAUVGYDUVOUVHENYDYEYFUVEUUGUQZYGUVOUVEUUGYFYDUV
      OYEYFUVAYKEUVTUVDXPPQYKCEGXQSYGUVCUVEUUGYFYDUVCYEUVDPQXRUVHYJBVRSWBUVGYDU
      VOYMUVIOUWNYGUVOUVEUUGUWBQYKCEGXSSXTWDWEYAYBYC $.
      $( [1-Oct-2009] $)

    $( Continuity in terms of interior.  (Contributed by Jeff Hankins,
       2-Oct-2009.) $)
    cnntr $p |- ( ( J e. Top /\ K e. Top /\ F : X --> Y )
       -> ( F e. ( J Cn K ) <-> A. y ( y C_ Y -> ( `' F " ( ( int ` K ) ` y ) )
            C_ ( ( int ` J ) ` ( `' F " y ) ) ) ) ) $=
      ( vo wcel cima wa wss cfv wi wb 3ad2ant3 wceq 3ad2antl2 syl2anc wf w3a co
      ctop ccn ccnv wral cnt wal iscn 3adant3 ibar simpl1 wfn cdm fndm cnvimass
      cv ffn sseq2 mpbii 3syl adantr ntropn imaeq2 eleq1d rcla4v ex com23 imp32
      ntrss2 adantrl imass2 ssntr syl22anc exp32 alrimdv syl5sseq simprr simpl2
      syl eltopss isopn3 mpbid imaeq2d sseq1 fveq2 fveq2d sseq12d cla4gv adantl
      fdm imbi12d mpid eqsstr3d eqssd mpbird ralrimdv impbid 3bitr2d ) CUDJZDUD
      JZEFBUAZUBZBCDUEUCJZXCBUFZIURZKZCJZIDUGZLZXJAURZFMZXFXLDUHNZNZKZXFXLKZCUH
      NZNZMZOZAUIZXAXBXEXKPXCIBCDEFGHUJUKXCXAXJXKPXBXCXJULQXDXJYBXDXJYAAXDXJXMX
      TXDXJXMLZLZXAXQEMZXPCJZXPXQMZXTXAXBXCYCUMXDYEYCXCXAYEXBXCBEUNBUOZERZYEEFB
      USEBUPYIXQYHMYEBXLUQYHEXQUTVAVBQVCXDXJXMYFXDXMXJYFXDXMXJYFOZXDXMLXODJZYJX
      BXAXMYKXCXLDFHVDSXIYFIXODXGXORXHXPCXGXOXFVEVFVGWAVHVIVJYDXOXLMZYGXDXMYLXJ
      XBXAXMYLXCXLDFHVKSVLXOXLXFVMWAXQCXPEGVNVOVPVQXDYBXIIDXDYBXGDJZXIXDYBYMLZL
      ZXIXHXRNZXHRZYOYPXHYOXAXHEMZYPXHMXAXBXCYNUMZXDYRYNXCXAYRXBXCYHXHEBXGUQEFB
      WLVRQVCZXHCEGVKTYOXHXFXGXNNZKZYPYOUUAXGXFYOYMUUAXGRZXDYBYMVSYOXBXGFMZYMUU
      CPXAXBXCYNVTXDYMUUDYBXBXAYMUUDXCXGDFHWBSZVLXGDFHWCTWDWEXDYBYMUUBYPMZXDYMY
      BUUFXDYMYBUUFOXDYMLYBUUDUUFUUEYMYBUUDUUFOZOXDYAUUGAXGDXLXGRZXMUUDXTUUFXLX
      GFWFUUHXPUUBXSYPUUHXOUUAXFXLXGXNWGWEUUHXQXHXRXLXGXFVEWHWIWMWJWKWNVHVIVJWO
      WPYOXAYRXIYQPYSYTXHCEGWCTWQVPWRWSWT $.
      $( [2-Oct-2009] $)
  $}

  $( Homeomorphisms preserve closedness.  (Contributed by Jeff Hankins,
     3-Jul-2009.) $)
  hmeoclda $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Homeo K ) ) /\ S e. (
  Clsd ` J ) ) -> ( F " S ) e. ( Clsd ` K ) ) $=
    ( ctop wcel chomeosm co w3a ccld cfv wa cima ccnv imacnvcnv ccn simp2 simp1
    hmeobc biimp3a simprd 3jca cnclima sylan syl5eqelr ) CEFZDEFZBCDGHFZIZACJKF
    ZLBAMBNZNAMZDJKZBAOUIUGUFUKDCPHFZIUJULUMFUIUGUFUNUFUGUHQUFUGUHRUIBCDPHFZUNU
    FUGUHUOUNLBCDSTUAUBAUKDCUCUDUE $.
    $( [3-Jun-2014] $) $( [3-Jul-2009] $)

  $( Homeomorphisms preserve closedness.  (Contributed by Jeff Hankins,
     3-Jul-2009.) $)
  hmeocldb $p |- ( ( ( J e. Top /\ K e. Top /\ F e. ( J Homeo K ) ) /\ S e. (
  Clsd ` K ) ) -> ( `' F " S ) e. ( Clsd ` J ) ) $=
    ( ctop wcel chomeosm co w3a ccnv ccld cima simp2 simp1 cnvhmpha 3impia 3jca
    cfv hmeoclda sylan ) CEFZDEFZBCDGHFZIZUBUABJZDCGHFZIADKRFUEALCKRFUDUBUAUFUA
    UBUCMUAUBUCNUAUBUCUFBCDOPQAUEDCST $.
    $( [3-Jul-2009] $)

  ${
    $d o x y J $.  $d o x y X $.
    dfcon2OLD.1 $e |- X = U. J $.
    $( An alternate definition of connectedness.  (Moved into main set.mm as
       ~ dfcon2 and may be deleted by mathbox owner, JGH. --NM 29-May-2014.) $)
    dfcon2OLD $p |- ( J e. Top -> ( J e. Con <-> A. x e. J A. y e. J ( ( x =/=
        (/)
    /\ y =/= (/) /\ ( x i^i y ) = (/) ) -> X =/= ( x u. y ) ) ) ) $=
      ( dfcon2 ) ABCDEF $.
      $( [9-Jul-2009] $)
  $}

  ${
    $d r s u v x y J $.  $d r s u v x y S $.  $d r s u v x y X $.
    connsubOLD.1 $e |- X = U. J $.
    $( Two equivalent ways of saying that a subspace topology is connected.
       (Moved into main set.mm as ~ connsub and may be deleted by mathbox
       owner, JGH. --NM 29-May-2014.) $)
    connsubOLD $p |- ( ( J e. Top /\ S C_ X )
        -> ( ( subSp ` <. S , J >. ) e. Con
    <-> A. x e. J A. y e. J ( ( ( x i^i S ) =/= (/) /\ ( y i^i S ) =/= (/) /\ (
    x i^i y ) C_ ( X \ S ) ) -> -. S C_ ( x u. y ) ) ) ) $=
      ( connsub ) ABCDEFG $.
      $( [15-Dec-2013] $) $( [9-Jul-2009] $)
  $}


