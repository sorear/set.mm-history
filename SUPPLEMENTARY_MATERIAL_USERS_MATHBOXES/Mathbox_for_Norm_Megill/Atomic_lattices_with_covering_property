$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Atomic lattices with covering property
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c <o $.
  $c Atoms $.
  $c AtLat $.
  $(
  $c AtsLat $.
  $)
  $c CvLat $.

  $( Extend class notation with covers relation. $)
  ccvr $a class <o $.

  $( Extend class notation with atoms. $)
  catm $a class Atoms $.

  $( Extend class notation with atomic lattices. $)
  cal $a class AtLat $.

  $( Extend class notation with atomistic lattices. $)
  $(
  cas $a class AtsLat $.
  $)

  $( Extend class notation with lattices with the covering property. $)
  clc $a class CvLat $.

  ${
    $d p z a b $.
    $( Def. 8.4.13 p. 209. $)
    $( Define the covers relation ("is covered by") for posets.  " ` a ` is
       covered by ` b ` " means that ` a ` is strictly less than ` b ` and
       there is nothing in between.  See ~ cvrval for the relation form. $)
    df-covers $a |- <o = ( p e. _V |-> { <. a , b >. | ( ( a e. ( Base ` p )
               /\ b e. ( Base ` p ) )
           /\ a ( lt ` p ) b /\ -. E. z e. ( Base ` p )
              ( a ( lt ` p ) z /\ z ( lt ` p ) b ) ) } ) $.

    $( Def. 8.4.14 p. 210. $)
    $( Define the class of poset atoms. $)
    df-ats $a |- Atoms = ( p e. _V |-> { a e. ( Base ` p ) |
                      ( 0. ` p ) ( <o ` p ) a } ) $.
  $}

  ${
    $d p x y z B $.  $d p x y z K $.  $d p S $.
    cvrfval.b $e |- B = ( Base ` K ) $.
    cvrfval.s $e |- S = ( lt ` K ) $.
    cvrfval.c $e |- C = ( <o ` K ) $.
    $( Value of covers relation "is covered by". $)
    cvrfval $p |- ( K e. A -> C = { <. x , y >. | ( ( x e. B /\ y e. B )
                  /\ x S y /\ -. E. z e. B ( x S z /\ z S y ) ) } ) $=
      ( vp wcel cvv cv wa wbr copab cfv cbs wrex wn wceq elex ccvr cplt syl6eqr
      w3a fveq2 eleq2d anbi12d breqd rexeqbidv notbid 3anbi123d opabbidv 3anass
      df-covers opabbii cxp fvex eqeltri xpex opabssxp ssexi fvmpt syl5eq syl )
      HDMHNMZFAOZEMZBOZEMZPZVJVLGQZVJCOZGQZVPVLGQZPZCEUAZUBZUHZABRZUCHDUDVIFHUE
      SWCKLHVJLOZTSZMZVLWEMZPZVJVLWDUFSZQZVJVPWIQZVPVLWIQZPZCWEUAZUBZUHZABRWCNU
      EWDHUCZWPWBABWQWHVNWJVOWOWAWQWFVKWGVMWQWEEVJWQWEHTSZEWDHTUIIUGZUJWQWEEVLW
      SUJUKWQWIGVJVLWQWIHUFSGWDHUFUIJUGZULWQWNVTWQWMVSCWEEWSWQWKVQWLVRWQWIGVJVP
      WTULWQWIGVPVLWTULUKUMUNUOUPCLABURWCVNVOWAPZPZABRZNWBXBABVNVOWAUQUSXCEEUTE
      EEWRNIHTVAVBZXDVCXAABEEVDVEVBVFVGVH $.
      $( [18-Sep-2011] $)

    $d x y S $.  $d x y z X $.  $d x y z Y $.
    $( Binary relation expressing ` B ` covers ` A ` , which means that ` B `
       is larger than ` A ` and there is nothing in between.  Definition 3.2.18
       of [PtakPulmannova] p. 68.  ( ~ cvbr analog.) $)
    cvrval $p |- ( ( K e. A /\ X e. B /\ Y e. B ) -> ( X C Y <->
               ( X S Y /\ -. E. z e. B ( X S z /\ z S Y ) ) ) ) $=
      ( vx vy wcel w3a wbr cv wa wrex wn copab wb cvrfval 3anass opabbii syl6eq
      breqd 3ad2ant1 cop df-br breq1 anbi1d rexbidv notbid anbi12d breq2 anbi2d
      wceq opelopab2 syl5bb 3adant1 bitrd ) FBNZGCNZHCNZOGHDPZGHLQZCNMQZCNRZVGV
      HEPZVGAQZEPZVKVHEPZRZACSZTZRZRZLMUAZPZGHEPZGVKEPZVKHEPZRZACSZTZRZVCVDVFVT
      UBVEVCDVSGHVCDVIVJVPOZLMUAVSLMABCDEFIJKUCWHVRLMVIVJVPUDUEUFUGUHVDVEVTWGUB
      VCVTGHUIVSNVDVERWGGHVSUJVQGVHEPZWBVMRZACSZTZRWGLMGHCCVGGURZVJWIVPWLVGGVHE
      UKWMVOWKWMVNWJACWMVLWBVMVGGVKEUKULUMUNUOVHHURZWIWAWLWFVHHGEUPWNWKWEWNWJWD
      ACWNVMWCWBVHHVKEUPUQUMUNUOUSUTVAVB $.
      $( [18-Sep-2011] $)

    $( The covers relation implies the less-than relation.  ( ~ cvpss
       analog.) $)
    cvrlt $p |- ( ( ( K e. A /\ X e. B /\ Y e. B ) /\ X C Y ) -> X S Y ) $=
      ( vz wcel w3a wbr cv wa wrex wn cvrval simprbda ) EALFBLGBLMFGCNFGDNFKOZD
      NUAGDNPKBQRKABCDEFGHIJST $.
      $( [8-Oct-2011] $)

    $d z S $.  $d z Z $.
    $( There is no element between the two arguments of the covers relation.
       ( ~ cvnbtwn analog.) $)
    cvrnbtwn $p |- ( ( K e. A /\ ( X e. B /\ Y e. B /\ Z e. B ) /\ X C Y )
             -> -. ( X S Z /\ Z S Y ) ) $=
      ( vz wcel w3a wbr wa wn cv wrex wb cvrval 3adant3r3 wi ralnex breq2 breq1
      wral anbi12d notbid rcla4v syl5bir adantld 3ad2ant3 adantl sylbid 3impia
      wceq ) EAMZFBMZGBMZHBMZNZFGCOZFHDOZHGDOZPZQZURVBPVCFGDOZFLRZDOZVIGDOZPZLB
      SQZPZVGURUSUTVCVNTVALABCDEFGIJKUAUBVBVNVGUCZURVAUSVOUTVAVMVGVHVMVLQZLBUGV
      AVGVLLBUDVPVGLHBVIHUQZVLVFVQVJVDVKVEVIHFDUEVIHGDUFUHUIUJUKULUMUNUOUP $.
      $( [18-Oct-2011] $)
  $}

  ${
    ncvr1.b $e |- B = ( Base ` K ) $.
    ncvr1.u $e |- U = ( 1. ` K ) $.
    ncvr1.c $e |- C = ( <o ` K ) $.
    $( No element covers the lattice unit. $)
    ncvr1 $p |- ( ( K e. OP /\ X e. B ) -> -. U C X ) $=
      ( cops wcel wa wbr cplt cfv cple eqid ad2antrr simplr simpr syl31anc mt2d
      ople1 wn cpo opposet op1cl pltnle ex simpll cvrlt mtand ) DIJZEAJZKZCEBLZ
      CEDMNZLZUNUQECDONZLZACDUREFURPZGUBUNUQUSUCZUNUQKDUDJZCAJZUMUQVAULVBUMUQDU
      EQULVCUMUQACDFGUFZQULUMUQRUNUQSAUPDURCEFUTUPPZUGTUHUAUNUOKULVCUMUOUQULUMU
      OUIULVCUMUOVDQULUMUORUNUOSIABUPDCEFVEHUJTUK $.
      $( [8-Jul-2013] $)
  $}

  ${
    cvrletr.b $e |- B = ( Base ` K ) $.
    cvrletr.l $e |- .<_ = ( le ` K ) $.
    cvrletr.s $e |- S = ( lt ` K ) $.
    cvrletr.c $e |- C = ( <o ` K ) $.
    $( Property of an element above a covering. $)
    cvrletr $p |- ( ( K e. Poset /\ ( X e. B /\ Y e. B /\ Z e. B ) )
               -> ( ( X C Y /\ Y .<_ Z ) -> X S Z ) ) $=
      ( cpo wcel w3a wa wbr simpll simplr1 simplr2 simpr cvrlt syl31anc pltletr
      wi adantr mpand expimpd ) DMNZFANZGANZHANZOZPZFGBQZGHEQZFHCQZUNUOPZFGCQZU
      PUQURUIUJUKUOUSUIUMUORUJUKULUIUOSUJUKULUIUOTUNUOUAMABCDFGIKLUBUCUNUSUPPUQ
      UEUOACDEFGHIJKUDUFUGUH $.
      $( [7-Dec-2012] $)

    $d z A $.  $d z B $.  $d z K $.  $d z X $.  $d z Y $.
    $( Binary relation expressing ` Y ` covers ` X ` .  Definition of covers in
       [Kalmbach] p. 15.  ( ~ cvbr2 analog.) $)
    cvrval2 $p |- ( ( K e. A /\ X e. B /\ Y e. B ) -> ( X C Y <->
               ( X S Y /\ A. z e. B ( ( X S z /\ z .<_ Y ) -> z = Y ) ) ) ) $=
      ( wcel wbr wa wn wral wb anbi2d w3a cv wrex wceq wi cvrval wne iman df-ne
      anbi2i xchbinxr pltval 3com23 3expa anass syl6rbbr notbid syl5bb ralbidva
      ralnex syl6bb 3adant2 bitr4d ) FBNZHCNZICNZUAHIDOHIEOZHAUBZEOZVHIEOZPZACU
      CQZPZVGVIVHIGOZPZVHIUDZUEZACRZPZABCDEFHIJLMUFVDVFVSVMSVEVDVFPZVRVLVGVTVRV
      KQZACRVLVTVQWAACVQVOVHIUGZPZQVTVHCNZPZWAVQVOVPQZPWCVOVPUHWBWFVOVHIUIUJUKW
      EWCVKWEVKVIVNWBPZPWCWEVJWGVIVDVFWDVJWGSZVDWDVFWHBCCEFGVHIKLULUMUNTVIVNWBU
      OUPUQURUSVKACUTVATVBVC $.
      $( [16-Nov-2011] $)

    $( The covers relation implies no in-betweenness.  ( ~ cvnbtwn2 analog.) $)
    cvrnbtwn2 $p |- ( ( K e. Poset /\ ( X e. B /\ Y e. B /\ Z e. B )
                        /\ X C Y )
             -> ( ( X S Z /\ Z .<_ Y ) <-> Z = Y ) ) $=
      ( cpo wcel w3a wbr wa wi wn 3impia wceq cvrnbtwn 3expia iman wne wb simpl
      simpr3 simpr2 pltval syl3anc anbi2i syl6bb anbi2d syl6rbbr notbid syl5rbb
      df-ne anass sylibd cvrlt ex 3adant3r3 syl5ibrcom posref 3ad2antr2 3adant3
      breq2 breq1 jcad impbid ) DMNZFANZGANZHANZOZFGBPZOZFHCPZHGEPZQZHGUAZVLVPV
      QWAWBRZVLVPQZVQVSHGCPZQZSZWCVLVPVQWGMABCDFGHIKLUBUCWCWAWBSZQZSWDWGWAWBUDW
      DWIWFWDWFVSVTWHQZQWIWDWEWJVSWDWEVTHGUEZQZWJWDVLVOVNWEWLUFVLVPUGVLVMVNVOUH
      VLVMVNVOUIMAACDEHGJKUJUKWKWHVTHGURULUMUNVSVTWHUSUOUPUQUTTVRWBVSVTVRVSWBFG
      CPZVLVPVQWMVLVMVNVQWMRVOVLVMVNOVQWMMABCDFGIKLVAVBVCTHGFCVHVDVLVPWBVTRVQWD
      VTWBGGEPZVLVMVNWNVOADEGIJVEVFHGGEVIVDVGVJVK $.
      $( [17-Nov-2011] $)

    $( The covers relation implies no in-betweenness.  ( ~ cvnbtwn3 analog.) $)
    cvrnbtwn3 $p |- ( ( K e. Poset /\ ( X e. B /\ Y e. B /\ Z e. B )
                        /\ X C Y )
             -> ( ( X .<_ Z /\ Z S Y ) <-> X = Z ) ) $=
      ( cpo wcel w3a wbr wa wn wi 3adant3 wceq cvrnbtwn wne wb pltval 3adant3r2
      anbi1d notbid an32 df-ne anbi2i bitri notbii iman bitr4i syl6bb syl5ibcom
      mpbid posref breq2 3ad2antr1 simp1 simp21 simp3 cvrlt syl31anc breq1 jcad
      simp22 impbid ) DMNZFANZGANZHANZOZFGBPZOZFHEPZHGCPZQZFHUAZVQFHCPZVSQZRZVT
      WASZMABCDFGHIKLUBVQWDVRFHUCZQZVSQZRZWEVQWCWHVQWBWGVSVKVOWBWGUDZVPVKVLVNWJ
      VMMAACDEFHJKUEUFTUGUHWIVTWARZQZRWEWHWLWHVTWFQWLVRWFVSUIWFWKVTFHUJUKULUMVT
      WAUNUOUPURVQWAVRVSVKVOWAVRSZVPVKVMVLWMVNVKVLQFFEPWAVRADEFIJUSFHFEUTUQVATV
      QFGCPZWAVSVQVKVLVMVPWNVKVOVPVBVKVLVMVNVPVCVKVLVMVNVPVIVKVOVPVDMABCDFGIKLV
      EVFFHGCVGUQVHVJ $.
      $( [4-Nov-2011] $)
  $}

  ${
    $d x y B $.  $d x y O $.  $d x y K $.  $d x y X $.  $d x y Y $.
    cvrcon3b.b $e |- B = ( Base ` K ) $.
    cvrcon3b.o $e |- O = ( oc ` K ) $.
    cvrcon3b.c $e |- C = ( <o ` K ) $.
    $( Contraposition law for the covers relation.  ( ~ cvcon3 analog.) $)
    cvrcon3b $p |- ( ( K e. OP /\ X e. B /\ Y e. B )
                        -> ( X C Y <-> ( O ` Y ) C ( O ` X ) ) ) $=
      ( vx vy cops wcel cfv wbr wa wb syl3anc anbi12d opoccl w3a cplt wrex eqid
      cv wn opltcon3b simpl1 simpl2 simpr wi 3ad2antl1 wceq breq2 breq1 rcla4ev
      simpl3 ex sylbid rexlimdva opltcon1b opltcon2b impbid notbid cvrval simp1
      syl ancomsd 3adant2 3adant3 3bitr4d ) CLMZEAMZFAMZUAZEFCUBNZOZEJUEZVPOZVR
      FVPOZPZJAUCZUFZPFDNZEDNZVPOZWDKUEZVPOZWGWEVPOZPZKAUCZUFZPZEFBOWDWEBOZVOVQ
      WFWCWLAVPCDEFGVPUDZHUGVOWBWKVOWBWKVOWAWKJAVOVRAMZPZWAVRDNZWEVPOZWDWRVPOZP
      WKWQVSWSVTWTWQVLVMWPVSWSQVLVMVNWPUHZVLVMVNWPUIVOWPUJZAVPCDEVRGWOHUGRWQVLW
      PVNVTWTQXAXBVLVMVNWPUQAVPCDVRFGWOHUGRSWQWTWSWKWQWRAMZWTWSPZWKUKVLVMWPXCVN
      ACDVRGHTULXCXDWKWJXDKWRAWGWRUMWHWTWIWSWGWRWDVPUNWGWRWEVPUOSUPURVGVHUSUTVO
      WJWBKAVOWGAMZPZWJWGDNZFVPOZEXGVPOZPWBXFWHXHWIXIXFVLVNXEWHXHQVLVMVNXEUHZVL
      VMVNXEUQVOXEUJZAVPCDFWGGWOHVARXFVLXEVMWIXIQXJXKVLVMVNXEUIAVPCDWGEGWOHVBRS
      XFXIXHWBXFXGAMZXIXHPZWBUKVLVMXEXLVNACDWGGHTULXLXMWBWAXMJXGAVRXGUMVSXIVTXH
      VRXGEVPUNVRXGFVPUOSUPURVGVHUSUTVCVDSJLABVPCEFGWOIVEVOVLWDAMZWEAMZWNWMQVLV
      MVNVFVLVNXNVMACDFGHTVIVLVMXOVNACDEGHTVJKLABVPCWDWEGWOIVERVK $.
      $( [4-Nov-2011] $)
  $}

  ${
    cvrle.b $e |- B = ( Base ` K ) $.
    cvrle.l $e |- .<_ = ( le ` K ) $.
    cvrle.c $e |- C = ( <o ` K ) $.
    $( The covers relation implies the less-than-or-equal relation. $)
    cvrle $p |- ( ( ( K e. A /\ X e. B /\ Y e. B ) /\ X C Y ) -> X .<_ Y ) $=
      ( wcel w3a wbr cplt cfv eqid cvrlt wne pltval simprbda syldan ) DAKFBKGBK
      LZFGCMFGDNOZMZFGEMZABCUCDFGHUCPZJQUBUDUEFGRABBUCDEFGIUFSTUA $.
      $( [12-Oct-2011] $)

    $( The covers relation implies no in-betweenness.  Part of proof of Lemma
       7.5.1 of [MaedaMaeda] p. 31.  ( ~ cvnbtwn4 analog.) $)
    cvrnbtwn4 $p |- ( ( K e. Poset /\ ( X e. B /\ Y e. B /\ Z e. B )
                        /\ X C Y )
             -> ( ( X .<_ Z /\ Z .<_ Y ) <-> ( X = Z \/ Z = Y ) ) ) $=
      ( cpo wcel w3a wbr wa wceq wn wi wb wne wo cplt eqid cvrnbtwn iman pltval
      cfv 3adant3r2 3com23 3adant3r1 anbi12d neanior anbi2i an4 bitr3i syl6rbbr
      notbid syl5rbb mpbid posref 3ad2antr3 breq1 syl5ibrcom cvrle ex 3adant3r3
      3adant3 3impia breq2 jaod syl5ibcom jcad impbid ) CKLZEALZFALZGALZMZEFBNZ
      MZEGDNZGFDNZOZEGPZGFPZUAZVTEGCUBUGZNZGFWGNZOZQZWCWFRZKABWGCEFGHWGUCZJUDVN
      VRWKWLSVSWLWCWFQZOZQVNVROZWKWCWFUEWPWOWJWPWJWAEGTZOZWBGFTZOZOZWOWPWHWRWIW
      TVNVOVQWHWRSVPKAAWGCDEGIWMUFUHVNVPVQWIWTSZVOVNVQVPXBKAAWGCDGFIWMUFUIUJUKW
      OWCWQWSOZOXAXCWNWCEGGFULUMWAWBWQWSUNUOUPUQURVGUSVTWFWAWBVTWDWAWEVTWAWDGGD
      NZVNVRXDVSVNVOVQXDVPACDGHIUTVAVGZEGGDVBVCVTWAWEEFDNZVNVRVSXFVNVOVPVSXFRVQ
      VNVOVPMVSXFKABCDEFHIJVDVEVFVHZGFEDVIVCVJVTWDWBWEVTXFWDWBXGEGFDVBVKVTXDWEW
      BXEGFGDVIVKVJVLVM $.
      $( [18-Oct-2011] $)

    $( The covers relation implies the negation of the reverse
       less-than-or-equal relation. $)
    cvrnle $p |- ( ( ( K e. Poset /\ X e. B /\ Y e. B ) /\ X C Y )
                     -> -. Y .<_ X ) $=
      ( cpo wcel w3a wbr cplt cfv wn eqid cvrlt pltnle syldan ) CJKEAKFAKLEFBME
      FCNOZMFEDMPJABUACEFGUAQZIRAUACDEFGHUBST $.
      $( [18-Oct-2011] $)
  $}

  ${
    cvrne.b $e |- B = ( Base ` K ) $.
    cvrne.c $e |- C = ( <o ` K ) $.
    $( The covers relation implies inequality. $)
    cvrne $p |- ( ( ( K e. A /\ X e. B /\ Y e. B ) /\ X C Y ) -> X =/= Y ) $=
      ( wcel w3a wbr cplt cfv wne eqid cvrlt cple pltval simplbda syldan ) DAIE
      BIFBIJZEFCKEFDLMZKZEFNZABCUBDEFGUBOZHPUAUCEFDQMZKUDABBUBDUFEFUFOUERST $.
      $( [13-Oct-2011] $)

    $( The covers relation is not reflexive.  ( ~ cvnref analog.) $)
    cvrnref $p |- ( ( K e. A /\ X e. B ) -> -. X C X ) $=
      ( wcel wa wceq wbr wn eqid wne simpll simplr simpr cvrne syl31anc ex mpi
      necon2bd ) DAHZEBHZIZEEJEECKZLEMUEUFEEUEUFEENZUEUFIUCUDUDUFUGUCUDUFOUCUDU
      FPZUHUEUFQABCDEEFGRSTUBUA $.
      $( [1-Nov-2012] $)
  $}

  ${
    cvrcmp.b $e |- B = ( Base ` K ) $.
    cvrcmp.l $e |- .<_ = ( le ` K ) $.
    cvrcmp.c $e |- C = ( <o ` K ) $.
    $( If two lattice elements that cover a third are comparable, then they are
       equal. $)
    cvrcmp $p |- ( ( K e. Poset /\ ( X e. B /\ Y e. B /\ Z e. B ) /\
          ( Z C X /\ Z C Y ) ) -> ( X .<_ Y <-> X = Y ) ) $=
      ( cpo wcel w3a wbr wa wceq wne simpl1 simpl23 syl31anc simpl21 simpl3l wo
      cvrne wi wb simpl22 simpl3r cvrnbtwn4 syl131anc cvrle simpr mpbi2and neor
      sylib mpd ex simp1 simp21 posref syl2anc breq2 syl5ibcom impbid ) CKLZEAL
      ZFALZGALZMZGEBNZGFBNZOZMZEFDNZEFPZVMVNVOVMVNOZGEQZVOVPVEVHVFVJVQVEVIVLVNR
      ZVFVGVHVEVLVNSZVFVGVHVEVLVNUAZVJVKVEVIVNUBZKABCGEHJUDTVPGEPVOUCZVQVOUEVPG
      EDNZVNWBVPVEVHVGVFVKWCVNOWBUFVRVSVFVGVHVEVLVNUGVTVJVKVEVIVNUHABCDGFEHIJUI
      UJVPVEVHVFVJWCVRVSVTWAKABCDGEHIJUKTVMVNULUMVOGEUNUOUPUQVMEEDNZVOVNVMVEVFW
      DVEVIVLURVEVFVGVHVLUSACDEHIUTVAEFEDVBVCVD $.
      $( [6-Feb-2012] $)

    $( If two lattice elements covered by a third are comparable, then they are
       equal. $)
    cvrcmp2 $p |- ( ( K e. OP /\ ( X e. B /\ Y e. B /\ Z e. B ) /\
          ( X C Z /\ Y C Z ) ) -> ( X .<_ Y <-> X = Y ) ) $=
      ( wcel w3a wbr wa cfv wceq wb opoccl syl2anc cvrcon3b cops 3ad2ant1 simp1
      coc opposet simp22 eqid simp21 simp23 3adant3r2 3adant3r1 anbi12d biimp3a
      cpo ancomd cvrcmp syl131anc oplecon3b syl3anc opcon3b 3bitr4d ) CUAKZEAKZ
      FAKZGAKZLZEGBMZFGBMZNZLZFCUDOZOZEVKOZDMZVLVMPZEFDMZEFPZVJCUNKZVLAKZVMAKZG
      VKOZAKZWAVLBMZWAVMBMZNVNVOQVBVFVRVICUEUBVJVBVDVSVBVFVIUCZVBVCVDVEVIUFZACV
      KFHVKUGZRSVJVBVCVTWEVBVCVDVEVIUHZACVKEHWGRSVJVBVEWBWEVBVCVDVEVIUIACVKGHWG
      RSVJWDWCVBVFVIWDWCNVBVFNVGWDVHWCVBVCVEVGWDQVDABCVKEGHWGJTUJVBVDVEVHWCQVCA
      BCVKFGHWGJTUKULUMUOABCDVLVMWAHIJUPUQVJVBVCVDVPVNQWEWHWFACDVKEFHIWGURUSVJV
      BVCVDVQVOQWEWHWFACVKEFHWGUTUSVA $.
      $( [20-Jun-2012] $)
  $}

  ${
    $d p x B $.  $d p C $.  $d p x K $.  $d p Z $.
    patoms.b $e |- B = ( Base ` K ) $.
    patoms.z $e |- Z = ( 0. ` K ) $.
    patoms.c $e |- C = ( <o ` K ) $.
    patoms.a $e |- A = ( Atoms ` K ) $.
    $( The set of atoms in a poset. $)
    pats $p |- ( K e. D -> A = { x e. B | Z C x } ) $=
      ( vp cvv wbr cfv cp0 ccvr cbs fveq2 syl6eqr wcel cv crab wceq elex breq1d
      catm breqd bitrd rabeqbidv df-ats fvex eqeltri rabex fvmpt syl5eq syl ) F
      EUAFMUAZBGAUBZDNZACUCZUDFEUEURBFUGOVAKLFLUBZPOZUSVBQOZNZAVBROZUCVAMUGVBFU
      DZVEUTAVFCVGVFFROZCVBFRSHTVGVEVCUSDNUTVGVDDVCUSVGVDFQODVBFQSJTUHVGVCGUSDV
      GVCFPOGVBFPSITUFUIUJLAUKUTACCVHMHFRULUMUNUOUPUQ $.
      $( [18-Sep-2011] $)
  $}

  ${
    $d x B $.  $d x C $.  $d x K $.  $d x P $.  $d x Z $.
    isatom.b $e |- B = ( Base ` K ) $.
    isatom.z $e |- Z = ( 0. ` K ) $.
    isatom.c $e |- C = ( <o ` K ) $.
    isatom.a $e |- A = ( Atoms ` K ) $.
    $( The predicate "is an atom".  ( ~ ela analog.) $)
    isat $p |- ( K e. D -> ( P e. A <-> ( P e. B /\ Z C P ) ) ) $=
      ( vx wcel cv wbr crab wa pats eleq2d breq2 elrab syl6bb ) FDMZEAMEGLNZCOZ
      LBPZMEBMGECOZQUCAUFELABCDFGHIJKRSUEUGLEBUDEGCTUAUB $.
      $( [18-Sep-2011] $)

    $( The predicate "is an atom".  ( ~ elatcv0 analog.) $)
    isat2 $p |- ( ( K e. D /\ P e. B ) -> ( P e. A <-> Z C P ) ) $=
      ( wcel wa wbr wb isat adantr ibar adantl bitr4d ) FDLZEBLZMEALZUBGECNZMZU
      DUAUCUEOUBABCDEFGHIJKPQUBUDUEOUAUBUDRST $.
      $( [18-Jun-2012] $)
  $}

  ${
    atomcvr0.z $e |- Z = ( 0. ` K ) $.
    atomcvr0.c $e |- C = ( <o ` K ) $.
    atomcvr0.a $e |- A = ( Atoms ` K ) $.
    $( An atom covers zero.  ( ~ atcv0 analog.) $)
    atcvr0 $p |- ( ( K e. D /\ P e. A ) -> Z C P ) $=
      ( wcel cbs cfv wbr eqid isat simplbda ) ECJDAJDEKLZJFDBMAQBCDEFQNGHIOP $.
      $( [4-Nov-2011] $)
  $}

  ${
    atombase.b $e |- B = ( Base ` K ) $.
    atombase.a $e |- A = ( Atoms ` K ) $.
    $( An atom is a member of the lattice base set (i.e. a lattice element).
       ( ~ atelch analog.) $)
    atbase $p |- ( P e. A -> P e. B ) $=
      ( cvv wcel catm cfv c0 wceq n0i eqeq1i sylnib fvprc nsyl2 cp0 ccvr eqid
      wbr isat simprbda mpancom ) DGHZCAHZCBHZUFDIJZKLZUEUFAKLUIACMAUHKFNODIPQU
      EUFUGDRJZCDSJZUAABUKGCDUJEUJTUKTFUBUCUD $.
      $( [10-Oct-2011] $)

    $d x A $.  $d x B $.
    $( The set of atoms is a subset of the base set.  ( ~ atssch analog.) $)
    atssbase $p |- A C_ B $=
      ( vx cv atbase ssriv ) FABABFGCDEHI $.
      $( [21-Oct-2011] $)
  $}

  ${
    0ltat.z $e |- Z = ( 0. ` K ) $.
    0ltat.s $e |- S = ( lt ` K ) $.
    0ltat.a $e |- A = ( Atoms ` K ) $.
    $( An atom is greater than zero. $)
    0ltat $p |- ( ( K e. OP /\ P e. A ) -> Z S P ) $=
      ( cops wcel wa cbs cfv ccvr wbr simpl eqid op0cl adantr atbase syl31anc
      adantl atcvr0 cvrlt ) DIJZBAJZKUEEDLMZJZBUGJZEBDNMZOEBCOUEUFPUEUHUFUGDEUG
      QZFRSUFUIUEAUGBDUKHTUBAUJIBDEFUJQZHUCIUGUJCDEBUKGULUDUA $.
      $( [4-Jul-2012] $)
  $}

  ${
    leatom.b $e |- B = ( Base ` K ) $.
    leatom.l $e |- .<_ = ( le ` K ) $.
    leatom.z $e |- Z = ( 0. ` K ) $.
    leatom.a $e |- A = ( Atoms ` K ) $.
    $( A poset element less than or equal to an atom equals either zero or the
       atom.  ( ~ atss analog.) $)
    leatb $p |- ( ( K e. OP /\ X e. B /\ P e. A )
                      -> ( X .<_ P <-> ( X = P \/ X = Z ) ) ) $=
      ( cops wcel w3a wbr wceq wo wa op0le syl6bb 3adant3 biantrurd cpo ccvr wb
      cfv opposet 3ad2ant1 op0cl atbase id 3anim123i 3com23 eqid atcvr0 3adant2
      cvrnbtwn4 syl3anc eqcom orbi1i bitrd orcom ) DLMZFBMZCAMZNZFCEOZFGPZFCPZQ
      ZVIVHQVFVGGFEOZVGRZVJVFVKVGVCVDVKVEBDEFGHIJSUAUBVFVLGFPZVIQZVJVFDUCMZGBMZ
      CBMZVDNZGCDUDUFZOZVLVNUEVCVDVOVEDUGUHVCVEVDVRVCVPVEVQVDVDBDGHJUIABCDHKUJV
      DUKULUMVCVEVTVDAVSLCDGJVSUNZKUOUPBVSDEGCFHIWAUQURVMVHVIGFUSUTTVAVHVIVBT
      $.
      $( [17-Nov-2011] $)

    $( A poset element less than or equal to an atom equals either zero or the
       atom. $)
    leat $p |- ( ( ( K e. OP /\ X e. B /\ P e. A ) /\ X .<_ P )
                      -> ( X = P \/ X = Z ) ) $=
      ( cops wcel w3a wbr wceq wo leatb biimpa ) DLMFBMCAMNFCEOFCPFGPQABCDEFGHI
      JKRS $.
      $( [15-Oct-2013] $)

    $( A nonzero poset element less than or equal to an atom equals the
       atom. $)
    leat2 $p |- ( ( ( K e. OP /\ X e. B /\ P e. A ) /\ ( X =/= Z /\ X .<_ P ) )
                      -> X = P ) $=
      ( cops wcel w3a wne wbr wceq wi wo leatb orcom bitri syl6bb biimpd com23
      neor imp32 ) DLMFBMCAMNZFGOZFCEPZFCQZUHUJUIUKUHUJUIUKRZUHUJUKFGQZSZULABCD
      EFGHIJKTUNUMUKSULUKUMUAUKFGUFUBUCUDUEUG $.
      $( [6-Mar-2013] $)

    $( A poset element less than or equal to an atom is either an atom or
       zero. $)
    leat3 $p |- ( ( ( K e. OP /\ X e. B /\ P e. A ) /\ X .<_ P )
                      -> ( X e. A \/ X = Z ) ) $=
      ( cops wcel w3a wbr wa wceq wo leat wi simpl3 eleq1a syl orim1d mpd ) DLM
      ZFBMZCAMZNFCEOZPZFCQZFGQZRFAMZULRABCDEFGHIJKSUJUKUMULUJUHUKUMTUFUGUHUIUAC
      AFUBUCUDUE $.
      $( [2-Dec-2012] $)
  $}

  ${
    m.b $e |- B = ( Base ` K ) $.
    m.m $e |- ./\ = ( meet ` K ) $.
    m.z $e |- Z = ( 0. ` K ) $.
    m.a $e |- A = ( Atoms ` K ) $.
    $( The meet of any element with an atom is either the atom or zero. $)
    meetat $p |- ( ( K e. OL /\ X e. B /\ P e. A )
       -> ( ( X ./\ P ) = P \/ ( X ./\ P ) = Z ) ) $=
      ( col wcel w3a co cple cfv wceq 3ad2ant1 syl3anc wbr wo ollat simp2 simp3
      clat atbase syl eqid latmle2 cops wb olop latmcl leatb mpbid ) DLMZFBMZCA
      MZNZFCEOZCDPQZUAZVACRVAGRUBZUTDUFMZURCBMZVCUQURVEUSDUCSZUQURUSUDZUTUSVFUQ
      URUSUEZABCDHKUGUHZBDVBEFCHVBUIZIUJTUTDUKMZVABMZUSVCVDULUQURVLUSDUMSUTVEUR
      VFVMVGVHVJBDEFCHIUNTVIABCDVBVAGHVKJKUOTUP $.
      $( [28-Aug-2012] $)

    $( The meet of any element with an atom is either the atom or zero. $)
    meetat2 $p |- ( ( K e. OL /\ X e. B /\ P e. A )
       -> ( ( X ./\ P ) e. A \/ ( X ./\ P ) = Z ) ) $=
      ( col wcel w3a co wceq wo meetat wi eleq1a 3ad2ant3 orim1d mpd ) DLMZFBMZ
      CAMZNZFCEOZCPZUHGPZQUHAMZUJQABCDEFGHIJKRUGUIUKUJUFUDUIUKSUECAUHTUAUBUC $.
      $( [30-Aug-2012] $)
  $}

  ${
    $d k p x $.
    $( Def. 8.4.15 p. 210. $)
    $( Define the class of atomic lattices, in which every nonzero element is
       less than or equal to an atom. .  We also ensure the existence of a
       lattice zero, since a lattice by itself may not have a zero. $)
    df-atl $a |- AtLat = { k e. Lat | ( ( 0. ` k ) e. ( Base ` k )
      /\ A. x e. ( Base ` k ) ( x =/= ( 0. ` k )
       -> E. p e. ( Atoms ` k ) p ( le ` k ) x ) ) } $.

    $( Def. 8.4.16 p. 210. $)
    $( Define the class of atomistic lattices.  Every non-zero element
       is the join of all atoms less than it.  If an
        orthomodular lattice is atomic, then it is also atomistic
      (Beltrametti_Cassinelli p. 98). $)
    $(
    df-atslat $a |- AtsLat = { l e. Lat | A. a e. ( Base ` l )
         a = ( ( lub ` l ) ` { p e. ( Atoms ` l ) | p ( le ` l ) a } ) } $.
    $)
  $}

  ${
    $d k y A $.  $d k x B $.  $d k x y K $.  $d k .<_ $.  $d k Z $.
    isatlat.b $e |- B = ( Base ` K ) $.
    isatlat.l $e |- .<_ = ( le ` K ) $.
    isatlat.z $e |- Z = ( 0. ` K ) $.
    isatlat.a $e |- A = ( Atoms ` K ) $.
    $( The predicate "is an atomic lattice."  Every nonzero element is less
       than or equal to an atom. $)
    isatl $p |- ( K e. AtLat <-> ( K e. Lat /\ Z e. B
        /\ A. x e. B ( x =/= Z -> E. y e. A y .<_ x ) ) ) $=
      ( vk wcel cv wa cp0 cfv cbs fveq2 syl6eqr cal clat wne wbr wrex wral cple
      wi w3a catm wceq eleq12d neeq2d breqd rexeqbidv imbi12d raleqbidv anbi12d
      df-atl elrab2 3anass bitr4i ) EUAMEUBMZGDMZANZGUCZBNZVEFUDZBCUEZUHZADUFZO
      ZOVCVDVKUILNZPQZVMRQZMZVEVNUCZVGVEVMUGQZUDZBVMUJQZUEZUHZAVOUFZOVLLEUBUAVM
      EUKZVPVDWCVKWDVNGVODWDVNEPQGVMEPSJTZWDVOERQDVMERSHTZULWDWBVJAVODWFWDVQVFW
      AVIWDVNGVEWEUMWDVSVHBVTCWDVTEUJQCVMEUJSKTWDVRFVGVEWDVREUGQFVMEUGSITUNUOUP
      UQURALBUSUTVCVDVKVAVB $.
      $( [18-Sep-2011] $)
  $}

  ${
    $d x p K $.
    $( An atomic lattice is a lattice. $)
    atllat $p |- ( K e. AtLat -> K e. Lat ) $=
      ( vx vp cal wcel clat cp0 cfv cbs cv wne cple wbr catm wrex wi wral isatl
      eqid simp1bi ) ADEAFEAGHZAIHZEBJZUAKCJUCALHZMCANHZOPBUBQBCUEUBAUDUAUBSUDS
      UASUESRT $.
      $( [21-Oct-2011] $)
  $}

  ${
    $d x p K $.
    $( An atomic lattice is a poset. $)
    atlpos $p |- ( K e. AtLat -> K e. Poset ) $=
      ( cal wcel clat cpo atllat latpos syl ) ABCADCAECAFAGH $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d y A $.  $d x B $.  $d x y K $.
    isatlati.1 $e |- K e. Lat $.
    isatlati.b $e |- B = ( Base ` K ) $.
    isatlati.l $e |- .<_ = ( le ` K ) $.
    isatlati.z $e |- Z = ( 0. ` K ) $.
    isatlati.a $e |- A = ( Atoms ` K ) $.
    isatlati.6 $e |- Z e. B $.
    isatlati.7 $e |- ( ( x e. B /\ x =/= Z ) ->
                          E. y e. A y .<_ x ) $.
    $( Properties that determine an atomic lattice. $)
    isatli $p |- K e. AtLat $=
      ( cal wcel clat cv wne wbr wrex wi wral isatl ex rgen mpbir3an ) EOPEQPGD
      PARZGSZBRUHFTBCUAZUBZADUCABCDEFGIJKLUDHMUKADUHDPUIUJNUEUFUG $.
      $( [18-Sep-2011] $)
  $}

  ${
    $d x B $.  $d p x K $.
    atl0cl.b $e |- B = ( Base ` K ) $.
    atl0cl.z $e |- Z = ( 0. ` K ) $.
    $( An atomic lattice has a zero element.  We can use this in place of
       ~ op0cl for lattices without orthocomplements. $)
    atl0cl $p |- ( K e. AtLat -> Z e. B ) $=
      ( vx vp cal wcel clat cv wne cple cfv wbr catm wrex wi wral eqid simp2bi
      isatl ) BHIBJICAIFKZCLGKUCBMNZOGBPNZQRFASFGUEABUDCDUDTEUETUBUA $.
      $( [5-Nov-2012] $)
  $}

  ${
    atl0le.b $e |- B = ( Base ` K ) $.
    atl0le.l $e |- .<_ = ( le ` K ) $.
    atl0le.z $e |- Z = ( 0. ` K ) $.
    $( Orthoposet zero is less than or equal to any element.  ( ~ ch0le
       analog.) $)
    atl0le $p |- ( ( K e. AtLat /\ X e. B ) -> Z .<_ X ) $=
      ( cal wcel wbr atl0cl adantr cpo atlpos p0le syl3an1 3com23 mpd3an3 ) BIJ
      ZDAJZEAJZEDCKZTUBUAABEFHLMTUBUAUCTBNJUBUAUCBOABCDEFGHPQRS $.
      $( [12-Oct-2011] $)

    $( An element less than or equal to zero equals zero.  ( ~ chle0
       analog.) $)
    atlle0 $p |- ( ( K e. AtLat /\ X e. B ) -> ( X .<_ Z <-> X = Z ) ) $=
      ( cal wcel wa wbr wceq atl0le biantrud cpo wb atlpos adantr simpr syl3anc
      atl0cl posasymb bitrd ) BIJZDAJZKZDECLZUHEDCLZKZDEMZUGUIUHABCDEFGHNOUGBPJ
      ZUFEAJZUJUKQUEULUFBRSUEUFTUEUMUFABEFHUBSABCDEFGUCUAUD $.
      $( [21-Oct-2011] $)
  $}

  ${
    atlltne0.b $e |- B = ( Base ` K ) $.
    atlltne0.s $e |- S = ( lt ` K ) $.
    atlltne0.z $e |- Z = ( 0. ` K ) $.
    $( A lattice element greater than zero is nonzero. $)
    atlltn0 $p |- ( ( K e. AtLat /\ X e. B ) -> ( Z S X <-> X =/= Z ) ) $=
      ( cal wcel wa wbr cple cfv wne wb simpl atl0cl adantr simpr syl3anc necom
      eqid pltval atl0le biantrurd syl5rbb bitrd ) CIJZDAJZKZEDBLZEDCMNZLZEDOZK
      ZDEOZUKUIEAJZUJULUPPUIUJQUIURUJACEFHRSUIUJTIAABCUMEDUMUCZGUDUAUQUOUKUPDEU
      BUKUNUOACUMDEFUSHUEUFUGUH $.
      $( [1-Jun-2012] $)
  $}

  ${
    $d x B $.  $d x K $.  $d x P $.  $d x Z $.
    isat3.b $e |- B = ( Base ` K ) $.
    isat3.l $e |- .<_ = ( le ` K ) $.
    isat3.z $e |- Z = ( 0. ` K ) $.
    isat3.a $e |- A = ( Atoms ` K ) $.
    $( The predicate "is an atom".  ( ~ elat2 analog.) $)
    isat3 $p |- ( K e. AtLat -> ( P e. A <-> ( P e. B
        /\ P =/= Z /\ A. x e. B ( x .<_ P -> ( x = P \/ x = Z ) ) ) ) ) $=
      ( cal wcel wne wbr wceq wo wi wral wa cv w3a ccvr eqid isat cplt wb simpl
      atl0cl adantr cvrval2 syl3anc atlltn0 adantlr imbi1d imbi2d impexp bi2.04
      cfv simpr bitri orcom neor imbi2i 3bitr4g ralbidva bitr2d pm5.32da bitr4d
      anbi12d 3anass syl6bbr ) ELMZDBMZDCMZDGNZAUAZDFOZVQDPZVQGPZQZRZACSZTZTZVO
      VPWCUBVMVNVOGDEUCUSZOZTWEBCWFLDEGHJWFUDZKUEVMVOWDWGVMVOTZWGGDEUFUSZOZGVQW
      JOZVRTVSRZACSZTZWDWIVMGCMZVOWGWOUGVMVOUHVMWPVOCEGHJUIUJVMVOUTALCWFWJEFGDH
      IWJUDZWHUKULWIWKVPWNWCCWJEDGHWQJUMWIWMWBACWIVQCMZTZVRWLVSRZRZVRVQGNZVSRZR
      WMWBWSWTXCVRWSWLXBVSVMWRWLXBUGVOCWJEVQGHWQJUMUNUOUPWMWLVRVSRRXAWLVRVSUQWL
      VRVSURVAWAXCVRWAVTVSQXCVSVTVBVSVQGVCVAVDVEVFVJVGVHVIVOVPWCVKVL $.
      $( [27-Apr-2014] $)
  $}

  ${
    $d x K $.  $d x P $.  $d x Z $.
    atne0.z $e |- Z = ( 0. ` K ) $.
    atne0.a $e |- A = ( Atoms ` K ) $.
    $( An atom is not zero.  ( ~ atne0 analog.) $)
    atn0 $p |- ( ( K e. AtLat /\ P e. A ) -> P =/= Z ) $=
      ( vx cal wcel wne cbs cfv cv cple wbr wceq wo wi wral eqid w3a syl6bi imp
      isat3 simp2 ) CHIZBAIZBDJZUFUGBCKLZIZUHGMZBCNLZOUKBPUKDPQRGUISZUAUHGAUIBC
      ULDUITULTEFUDUJUHUMUEUBUC $.
      $( [5-Nov-2012] $)
  $}

  ${
    atnle0.l $e |- .<_ = ( le ` K ) $.
    atnle0.z $e |- Z = ( 0. ` K ) $.
    atnle0.a $e |- A = ( Atoms ` K ) $.
    $( An atom is not less than or equal to zero. $)
    atnle0 $p |- ( ( K e. AtLat /\ P e. A ) -> -. P .<_ Z ) $=
      ( cal wcel wa cpo cbs cfv ccvr wbr wn atlpos adantr eqid atl0cl syl31anc
      atbase adantl atcvr0 cvrnle ) CIJZBAJZKCLJZECMNZJZBUJJZEBCONZPBEDPQUGUIUH
      CRSUGUKUHUJCEUJTZGUASUHULUGAUJBCUNHUCUDAUMIBCEGUMTZHUEUJUMCDEBUNFUOUFUB
      $.
      $( [17-Oct-2011] $)
  $}

  ${
    atlen0.b $e |- B = ( Base ` K ) $.
    atlen0.l $e |- .<_ = ( le ` K ) $.
    atlen0.z $e |- Z = ( 0. ` K ) $.
    atlen0.a $e |- A = ( Atoms ` K ) $.
    $( A lattice element is nonzero if an atom is under it. $)
    atlen0 $p |- ( ( ( K e. AtLat /\ X e. B /\ P e. A ) /\ P .<_ X )
            -> X =/= Z ) $=
      ( cal wcel w3a wbr wa cplt cfv syl eqid simpl1 atl0cl simpl2 3jca syl2anc
      wne ccvr simpl3 atbase atcvr0 cvrlt syl31anc simpr cpo wi atlpos syl13anc
      pltletr mp2and pltne sylc necomd ) DLMZFBMZCAMZNZCFEOZPZGFVHVCGBMZVDNGFDQ
      RZOZGFUFVHVCVIVDVCVDVEVGUAZVHVCVIVLBDGHJUBSZVCVDVEVGUCZUDVHGCVJOZVGVKVHVC
      VICBMZGCDUGRZOZVOVLVMVHVEVPVCVDVEVGUHZABCDHKUISZVHVCVEVRVLVSAVQLCDGJVQTZK
      UJUELBVQVJDGCHVJTZWAUKULVFVGUMVHDUNMZVIVPVDVOVGPVKUOVHVCWCVLDUPSVMVTVNBVJ
      DEGCFHIWBURUQUSLBBVJDGFWBUTVAVB $.
      $( [26-May-2012] $)
  $}

  ${
    atcmp.l $e |- .<_ = ( le ` K ) $.
    atcmp.a $e |- A = ( Atoms ` K ) $.
    $( If two atoms are comparable, they are equal.  ( ~ atsseq analog.) $)
    atcmp $p |- ( ( K e. AtLat /\ P e. A /\ Q e. A ) -> ( P .<_ Q <-> P = Q ) )
        $=
      ( cal wcel w3a cpo cbs cfv cp0 ccvr wbr 3ad2ant1 eqid atbase atcvr0 wceq
      wb atlpos 3ad2ant2 3ad2ant3 atl0cl 3adant3 3adant2 cvrcmp syl132anc ) DHI
      ZBAIZCAIZJDKIZBDLMZIZCUOIZDNMZUOIZURBDOMZPZURCUTPZBCEPBCUAUBUKULUNUMDUCQU
      LUKUPUMAUOBDUORZGSUDUMUKUQULAUOCDVCGSUEUKULUSUMUODURVCURRZUFQUKULVAUMAUTH
      BDURVDUTRZGTUGUKUMVBULAUTHCDURVDVEGTUHUOUTDEBCURVCFVEUIUJ $.
      $( [13-Oct-2011] $)

    $( Frequently-used variation of ~ atcmp . $)
    atncmp $p |- ( ( K e. AtLat /\ P e. A /\ Q e. A )
             -> ( -. P .<_ Q <-> P =/= Q ) ) $=
      ( cal wcel w3a wbr atcmp necon3bbid ) DHIBAICAIJBCEKBCABCDEFGLM $.
      $( [29-Jun-2012] $)
  $}

  ${
    atnlt.s $e |- S = ( lt ` K ) $.
    atnlt.a $e |- A = ( Atoms ` K ) $.
    $( Two atoms cannot satisfy the less than relation. $)
    atnlt $p |- ( ( K e. AtLat /\ P e. A /\ Q e. A ) -> -. P S Q ) $=
      ( cal wcel w3a wbr wn wceq pltirr 3adant3 breq2 notbid syl5ibcom cple cfv
      eqid pltle atcmp sylibd necon3ad pm2.61dne ) EHIZBAIZCAIZJZBCDKZLZBCUJBBD
      KZLZBCMZULUGUHUNUIHADEBFNOUOUMUKBCBDPQRUJUKBCUJUKBCESTZKUOHAADEUPBCUPUAZF
      UBABCEUPUQGUCUDUEUF $.
      $( [7-Feb-2012] $)
  $}

  ${
    atcvreq0.b $e |- B = ( Base ` K ) $.
    atcvreq0.l $e |- .<_ = ( le ` K ) $.
    atcvreq0.z $e |- Z = ( 0. ` K ) $.
    atcvreq0.c $e |- C = ( <o ` K ) $.
    atcvreq0.a $e |- A = ( Atoms ` K ) $.
    $( An element covered by an atom must be zero.  ( ~ atcveq0 analog.) $)
    atcvreq0 $p |- ( ( K e. AtLat /\ X e. B /\ P e. A )
      -> ( X C P <-> X = Z ) ) $=
      ( cal wcel wbr wceq wa cfv adantr w3a cple cplt wb atlpos 3ad2ant1 atl0cl
      cpo atbase 3ad2ant3 simpl2 atcvr0 3adant2 eqid cvrnbtwn3 syl131anc atl0le
      3adant3 cvrlt syl3anl3 mpbi2and eqcomd ex breq1 syl5ibrcom impbid ) ENOZG
      BOZDAOZUAZGDCPZGHQZVJVKVLVJVKRZHGVMHGEUBSZPZGDEUCSZPZHGQZVMEUHOZHBOZDBOZV
      HHDCPZVOVQRVRUDVJVSVKVGVHVSVIEUEUFTVJVTVKVGVHVTVIBEHIKUGUFTVJWAVKVIVGWAVH
      ABDEIMUIZUJTVGVHVIVKUKVJWBVKVGVIWBVHACNDEHKLMULUMZTBCVPEVNHDGIVNUNZVPUNZL
      UOUPVJVOVKVGVHVOVIBEVNGHIWEKUQURTVIVGVHWAVKVQWCNBCVPEGDIWFLUSUTVAVBVCVJVK
      VLWBWDGHDCVDVEVF $.
      $( [4-Nov-2011] $)
  $}

  ${
    atncvr.c $e |- C = ( <o ` K ) $.
    atncvr.a $e |- A = ( Atoms ` K ) $.
    $( Two atoms cannot satisfy the covering relation. $)
    atncvr $p |- ( ( K e. AtLat /\ P e. A /\ Q e. A ) -> -. P C Q ) $=
      ( cal wcel w3a wbr wn cp0 cfv wne eqid atn0 3adant3 cbs wceq cple syl3an2
      wb atbase atcvreq0 necon3bbid mpbird ) EHIZCAIZDAIZJZCDBKZLCEMNZOZUHUIUNU
      JACEUMUMPZGQRUKULCUMUIUHCESNZIUJULCUMTUCAUPCEUPPZGUDAUPBDEEUANZCUMUQURPUO
      FGUEUBUFUG $.
      $( [7-Feb-2012] $)
  $}

  ${
    $d x y A $.  $d x B $.  $d x y K $.  $d x .<_ $.  $d x y X $.  $d x Z $.
    atlex.b $e |- B = ( Base ` K ) $.
    atlex.l $e |- .<_ = ( le ` K ) $.
    atlex.z $e |- Z = ( 0. ` K ) $.
    atlex.a $e |- A = ( Atoms ` K ) $.
    $( Every nonzero element of an atomic lattice is greater than or equal to
       an atom.  ( ~ hatomic analog.) $)
    atlex $p |- ( ( K e. AtLat /\ X e. B /\ X =/= Z ) -> E. y e. A y .<_ X ) $=
      ( vx cal wcel wne cv wbr wrex wi wral clat isatl simp3bi wceq neeq1 breq2
      rexbidv imbi12d rcla4cv syl 3imp ) DMNZFCNZFGOZAPZFEQZABRZULLPZGOZUOUREQZ
      ABRZSZLCTZUMUNUQSZSULDUANGCNVCLABCDEGHIJKUBUCVBVDLFCURFUDZUSUNVAUQURFGUEV
      EUTUPABURFUOEUFUGUHUIUJUK $.
      $( [21-Oct-2011] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y K $.  $d y .<_ $.  $d y ./\ $.  $d y P $.
    $d y X $.  $d y Z $.
    atnle.b $e |- B = ( Base ` K ) $.
    atnle.l $e |- .<_ = ( le ` K ) $.
    atnle.m $e |- ./\ = ( meet ` K ) $.
    atnle.z $e |- Z = ( 0. ` K ) $.
    atnle.a $e |- A = ( Atoms ` K ) $.
    $( Two ways of expressing "an atom is not less than or equal to a lattice
       element."  ( ~ atnssm0 analog.) $)
    atnle $p |- ( ( K e. AtLat /\ P e. A /\ X e. B ) ->
         ( -. P .<_ X <-> ( P ./\ X ) = Z ) ) $=
      ( vy wcel wbr wceq wa syl3anc wi cal w3a wn co cv wrex simpl1 clat atllat
      3ad2ant1 atbase 3ad2ant2 simp3 latmcl adantr simpr atlex wb adantl simpl2
      wne syl simpl3 latlem12 syl13anc atcmp breq1 biimpd imp3a sylbird adantlr
      syl6bi rexlimdva mpd necon1bd atn0 3adant3 latleeqm1 eqeq1 biimpcd sylbid
      ex necon3ad mpid impbid ) DUAOZCAOZGBOZUBZCGEPZUCZCGFUDZHQZWIWJWLHWIWLHVA
      ZWJWIWNRZNUEZWLEPZNAUFZWJWOWFWLBOZWNWRWFWGWHWNUGWIWSWNWIDUHOZCBOZWHWSWFWG
      WTWHDUIZUJZWGWFXAWHABCDIMUKZULZWFWGWHUMZBDFCGIKUNSUOWIWNUPNABDEWLHIJLMUQS
      WOWQWJNAWIWPAOZWQWJTWNWIXGRZWQWPCEPZWPGEPZRZWJXHWTWPBOZXAWHXKWQURXHWFWTWF
      WGWHXGUGZXBVBXGXLWIABWPDIMUKUSXHWGXAWFWGWHXGUTZXDVBWFWGWHXGVCBDEFWPCGIJKV
      DVEXHXIXJWJXHXIWPCQZXJWJTXHWFXGWGXIXOURXMWIXGUPXNAWPCDEJMVFSXOXJWJWPCGEVG
      VHVLVIVJVKVMVNWBVOWIWMCHVAZWKWFWGXPWHACDHLMVPVQWIWMXPWKTWIWMRZWJCHXQWJWLC
      QZCHQZWIWJXRURZWMWIWTXAWHXTXCXEXFBDEFCGIJKVRSUOWMXRXSTWIXRWMXSWLCHVSVTUSW
      AWCWBWDWE $.
      $( [5-Nov-2012] $)
  $}

  ${
    atnem0.m $e |- ./\ = ( meet ` K ) $.
    atnem0.z $e |- Z = ( 0. ` K ) $.
    atnem0.a $e |- A = ( Atoms ` K ) $.
    $( The meet of distinct atoms is zero.  ( ~ atnemeq0 analog.) $)
    atnem0 $p |- ( ( K e. AtLat /\ P e. A /\ Q e. A )
        -> ( P =/= Q <-> ( P ./\ Q ) = Z ) ) $=
      ( cal wcel w3a cple cfv wbr wn wne co wceq eqid atncmp cbs atbase syl3an3
      wb atnle bitr3d ) DJKZBAKZCAKZLBCDMNZOPZBCQBCERFSZABCDUKUKTZIUAUJUHUICDUB
      NZKULUMUEAUOCDUOTZIUCAUOBDUKECFUPUNGHIUFUDUG $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x K $.  $d x y .<_ $.  $d x U $.  $d x y X $.
    atlatmstc.b $e |- B = ( Base ` K ) $.
    atlatmstc.l $e |- .<_ = ( le ` K ) $.
    atlatmstc.u $e |- U = ( lub ` K ) $.
    atlatmstc.a $e |- A = ( Atoms ` K ) $.
    $( An atomic, complete, orthomodular lattice is atomistic i.e. every
       element is the join of the atoms under it.  See remark before
       Proposition 1 in [Kalmbach] p. 140; also remark in
       [BeltramettiCassinelli] p. 98.  ( ~ hatomistici analog.) $)
    atlatmstc $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. AtLat ) /\ X e. B )
           -> ( U ` { y e. A | y .<_ X } ) = X ) $=
      ( vx wcel wa wbr cfv wceq wss adantr wb coml ccla cal w3a cv crab cmee co
      coc cp0 simpl2 ssrab2 atssbase rabss2 ax-mp lubss mp3an23 atlpos 3ad2ant3
      syl lubid sylan breqtrd wrex wn wi breq1 elrab simpll2 sstri lubel mp3an3
      cpo sylancom syl5bir expdimp wne simpll3 eqid simpl3 atllat atbase adantl
      ex atn0 clat clatlubcl sylancl cops simpl1 omlop opoccl latlem12 syl13anc
      syl2anc opnoncon breq2d ople0 syl2an 3bitrd biimpa expr necon3ad mpd syld
      imnan sylib simplr mtbid nrexdv simpr latmcl syl3anc atlex omllaw3 mp2and
      necon1bd ) EUAMZEUBMZEUCMZUDZGCMZNZAUEZGFOZABUFZDPZGFOZGYGEUIPZPZEUGPZUHZ
      EUJPZQZYGGQZYCYGYEACUFZDPZGFYCXSYGYQFOZXRXSXTYBUKZXSYPCRYFYPRZYRYEACULBCR
      YTBCEHKUMZYEABCUNUOCYFYPDEFHIJUPUQUTYAEVMMZYBYQGQXTXRUUBXSEURUSACDEFGHIJV
      AVBVCYCLUEZYLFOZLBVDZVEYNYCUUDLBYCUUCBMZNZUUCGFOZUUCYJFOZNZUUDUUGUUHUUIVE
      ZVFUUJVEUUGUUHUUCYGFOZUUKYCUUFUUHUULUUFUUHNUUCYFMZYCUULYEUUHAUUCBYDUUCGFV
      GVHYCUUMUULYCUUMXSUULXRXSXTYBUUMVIXSUUMYFCRZUULYFBCYEABULUUAVJZCYFDEFUUCH
      IJVKVLVNWDVOVPUUGUULUUKUUGUULNZUUCYMVQZUUKUUGUUQUULYCUUFXTUUQXRXSXTYBUUFV
      RBUUCEYMYMVSZKWEVNSUUPUUIUUCYMUUGUULUUIUUCYMQZUUGUULUUINZUUSUUGUUTUUCYGYJ
      YKUHZFOZUUCYMFOZUUSUUGEWFMZUUCCMZYGCMZYJCMZUUTUVBTYCUVDUUFYCXTUVDXRXSXTYB
      VTEWAUTZSZUUFUVEYCBCUUCEHKWBZWCZYCUVFUUFYCXSUUNUVFYSUUOCYFDEHJWGWHZSYCUVG
      UUFYCEWIMZUVFUVGYCXRUVMXRXSXTYBWJZEWKUTZUVLCEYIYGHYIVSZWLWOZSZCEFYKUUCYGY
      JHIYKVSZWMWNYCUVBUVCTUUFYCUVAYMUUCFYCUVMUVFUVAYMQUVOUVLCEYKYIYGYMHUVPUVSU
      URWPWOWQSYCUVMUVEUVCUUSTUUFUVOUVJCEFUUCYMHIUURWRWSWTXAXBXCXDWDXEUUHUUIXFX
      GUUGUVDUVEYBUVGUUJUUDTUVIUVKYAYBUUFXHUVRCEFYKUUCGYJHIUVSWMWNXIXJYCUUEYLYM
      YCYLYMVQZUUEYCUVTNXTYLCMZUVTUUEXRXSXTYBUVTVRYCUWAUVTYCUVDYBUVGUWAUVHYAYBX
      KZUVQCEYKGYJHUVSXLXMSYCUVTXKLBCEFYLYMHIUURKXNXMWDXQXDYCXRUVFYBYHYNNYOVFUV
      NUVLUWBCEFYKYIYGGYMHIUVSUVPUURXOXMXP $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d p A $.  $d p B $.  $d p K $.  $d p .<_ $.  $d p X $.  $d p Y $.
    atlatle.b $e |- B = ( Base ` K ) $.
    atlatle.l $e |- .<_ = ( le ` K ) $.
    atlatle.a $e |- A = ( Atoms ` K ) $.
    $( The ordering of two Hilbert lattice elements is determined by the atoms
       under them.  ( ~ chrelat3 analog.) $)
    atlatle $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. AtLat )
                /\ X e. B /\ Y e. B )
         -> ( X .<_ Y <-> A. p e. A ( p .<_ X -> p .<_ Y ) ) ) $=
      ( wcel w3a wbr wi wa crab wss cfv wceq atlatmstc coml ccla cal cv simpl13
      wral cpo atlpos syl atbase adantl simpl2 simpl3 postr syl13anc exp3acom23
      ralrimdva ss2rab club simpl12 ssrab2 atssbase sstri lubss mp3an2 sylancom
      eqid ex 3adant3 3adant2 breq12d sylibd syl5bir impbid ) CUAKZCUBKZCUCKZLZ
      EBKZFBKZLZEFDMZGUDZEDMZWCFDMZNZGAUFZWAWBWFGAWAWCAKZOZWDWBWEWICUGKZWCBKZVS
      VTWDWBOWENWIVQWJVOVPVQVSVTWHUECUHUIWHWKWAABWCCHJUJUKVRVSVTWHULVRVSVTWHUMB
      CDWCEFHIUNUOUPUQWGWDGAPZWEGAPZQZWAWBWDWEGAURWAWNWLCUSRZRZWMWORZDMZWBWAWNW
      RWAWNVPWRVOVPVQVSVTWNUTVPWMBQWNWRWMABWEGAVAABCHJVBVCBWLWMWOCDHIWOVGZVDVEV
      FVHWAWPEWQFDVRVSWPESVTGABWOCDEHIWSJTVIVRVTWQFSVSGABWOCDFHIWSJTVJVKVLVMVN
      $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d p A $.  $d p B $.  $d p K $.  $d p .<_ $.  $d p X $.  $d p Y $.
    atlrelat1.b $e |- B = ( Base ` K ) $.
    atlrelat1.l $e |- .<_ = ( le ` K ) $.
    atlrelat1.s $e |- S = ( lt ` K ) $.
    atlrelat1.a $e |- A = ( Atoms ` K ) $.
    $( An atomistic lattice with 0 is relatively atomic.  Part of Lemma 7.2 of
       [MaedaMaeda] p. 30.  ( ~ chpssati , with ` /\ ` swapped, analog.) $)
    atlrelat1 $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. AtLat ) /\ X e. B /\ Y
        e. B ) -> ( X S Y
         -> E. p e. A ( -. p .<_ X /\ p .<_ Y ) ) ) $=
      ( coml wcel w3a wbr wn wa wi wral ccla cal cv cpo simp13 atlpos pltnle ex
      wrex syld3an1 iman ancom xchbinx ralbii wb atlatle 3com23 biimprd syl5bir
      syl con3d dfrex2 syl6ibr syld ) DMNZDUANZDUBNZOZFBNZGBNZOZFGCPZGFEPZQZHUC
      ZFEPZQZVOGEPZRZHAUIZDUDNZVIVHVJVLVNSVKVGWAVEVFVGVIVJUEDUFUTWAVIVJOVLVNBCD
      EFGIJKUGUHUJVKVNVSQZHATZQVTVKWCVMWCVRVPSZHATZVKVMWDWBHAWDVRVQRVSVRVPUKVRV
      QULUMUNVKVMWEVHVJVIVMWEUOABDEGFHIJLUPUQURUSVAVSHAVBVCVD $.
      $( [4-Dec-2011] $)
  $}

  ${
    $d k c a b $.
    $( Def. 8.4.17 p. 210. $)
    $( Define the class of lattices with the covering property. $)
    $(
    df-cvlat $a |- CvLat = { l e. AtLat | A. a e. ( Base ` l )
       A. p e. ( Atoms ` l ) ( ( a ( meet ` l ) p ) = ( 0. ` l )
          -> a ( <o ` l ) ( a ( join ` l ) p ) ) } $.
    $)

    $( Define the class of atomic lattices with the covering property.  (This
       is actually the exchange property, but they are equivalent.  The
       literature usually uses the covering property terminology.) $)
    df-cvlat $a |- CvLat = { k e. AtLat |
      A. a e. ( Atoms ` k ) A. b e. ( Atoms ` k ) A. c e. ( Base ` k )
            ( ( -. a ( le ` k ) c /\ a ( le ` k ) ( c ( join ` k ) b ) )
          -> b ( le ` k ) ( c ( join ` k ) a ) ) } $.
  $}

  $(
  @{
    @d l y A @.  @d l x B @.  @d l C @.  @d l .\/ @.  @d l x y K @.
    @d l ./\ @.  @d l Z @.
    iscvlat.1 @e |- B = ( Base ` K ) @.
    iscvlat.2 @e |- .\/ = ( join ` K ) @.
    iscvlat.3 @e |- ./\ = ( meet ` K ) @.
    iscvlat.4 @e |- Z = ( 0. ` K ) @.
    iscvlat.5 @e |- C = ( <o ` K ) @.
    iscvlat.6 @e |- A = ( Atoms ` K ) @.
    @( The predicate "is a lattice with the covering property". @)
    iscvlat @p |- ( K e. CvLat <-> ( K e. AtLat
      /\ A. x e. B A. y e. A ( ( x ./\ y ) = Z -> x C ( x .\/ y ) ) ) ) @=
      ( vl cv cmee cfv co cp0 wceq cjn ccvr wbr wi catm wral cbs cal clc fveq2
      syl6eqr oveqd eqeq12d wb breq syl breq2d bitrd imbi12d raleqbidv
      df-cvlat elrab2 ) AQZBQZPQZRSZTZVGUASZUBZVEVEVFVGUCSZTZVGUDSZUEZUFZBVGUGS
      ZUHZAVGUISZUHVEVFHTZIUBZVEVEVFFTZEUEZUFZBCUHZADUHPGUJUKVGGUBZVRWEAVSDWFVS
      GUISDVGGUIULJUMWFVPWDBVQCWFVQGUGSCVGGUGULOUMWFVKWAVOWCWFVIVTVJIWFVHHVEVFW
      FVHGRSHVGGRULLUMUNWFVJGUASIVGGUAULMUMUOWFVOVEVMEUEZWCWFVNEUBVOWGUPWFVNGUD
      SEVGGUDULNUMVEVMVNEUQURWFVMWBVEEWFVLFVEVFWFVLGUCSFVGGUCULKUMUNUSUTVAVBVBB
      APVCVD @.
      @( [18-Sep-2011] @)
  @}

  @{
    @d y A @.  @d x y B @.  @d x y K @.
    iscvlati.0 @e |- K e. AtLat @.
    iscvlati.1 @e |- B = ( Base ` K ) @.
    iscvlati.2 @e |- .\/ = ( join ` K ) @.
    iscvlati.3 @e |- ./\ = ( meet ` K ) @.
    iscvlati.4 @e |- Z = ( 0. ` K ) @.
    iscvlati.5 @e |- C = ( <o ` K ) @.
    iscvlati.6 @e |- A = ( Atoms ` K ) @.
    iscvlati.7 @e |- ( ( x e. B /\ y e. A /\ ( x ./\ y ) = Z )
                          -> x C ( x .\/ y ) ) @.
    @( Properties that a lattice with the covering property. @)
    iscvlati @p |- K e. CvLat @=
      ( clc wcel cal cv co wceq wbr wi wral iscvlat 3expia rgen2 mpbir2an ) GRS
      GTSAUAZBUAZHUBIUCZUKUKULFUBEUDZUEZBCUFADUFABCDEFGHIKLMNOPUGJUOABDCUKDSULC
      SUMUNQUHUIUJ @.
      @( [18-Sep-2011] @)
  @}
  $)

  ${
    $d k p q A $.  $d k x B $.  $d k .<_ $.  $d k .\/ $.  $d k p q x K $.
    iscvlat.b $e |- B = ( Base ` K ) $.
    iscvlat.l $e |- .<_ = ( le ` K ) $.
    iscvlat.j $e |- .\/ = ( join ` K ) $.
    iscvlat.a $e |- A = ( Atoms ` K ) $.
    $( The predicate "is an atomic lattice with the covering (or exchange)
       property". $)
    iscvlat $p |- ( K e. CvLat
       <-> ( K e. AtLat /\ A. p e. A A. q e. A A. x e. B
           ( ( -. p .<_ x /\ p .<_ ( x .\/ q ) ) -> q .<_ ( x .\/ p ) ) ) ) $=
      ( vk cv cfv wbr co wral fveq2 syl6eqr cple wn cjn wa wi cbs catm cal wceq
      clc breqd notbid eqidd breq123d anbi12d imbi12d raleqbidv df-cvlat elrab2
      oveqd ) HNZANZMNZUAOZPZUBZVAVBGNZVCUCOZQZVDPZUDZVGVBVAVHQZVDPZUEZAVCUFOZR
      ZGVCUGOZRZHVQRVAVBFPZUBZVAVBVGDQZFPZUDZVGVBVADQZFPZUEZACRZGBRZHBRMEUHUJVC
      EUIZVRWHHVQBWIVQEUGOBVCEUGSLTZWIVPWGGVQBWJWIVNWFAVOCWIVOEUFOCVCEUFSITWIVK
      WCVMWEWIVFVTVJWBWIVEVSWIVDFVAVBWIVDEUAOFVCEUASJTZUKULWIVAVAVIWAVDFWIVAUMW
      KWIVHDVBVGWIVHEUCODVCEUCSKTZUTUNUOWIVGVGVLWDVDFWIVGUMWKWIVHDVBVAWLUTUNUPU
      QUQUQMHGAURUS $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d p q x A $.  $d x B $.  $d p q x K $.
    iscvlat2.b $e |- B = ( Base ` K ) $.
    iscvlat2.l $e |- .<_ = ( le ` K ) $.
    iscvlat2.j $e |- .\/ = ( join ` K ) $.
    iscvlat2.m $e |- ./\ = ( meet ` K ) $.
    iscvlat2.z $e |- Z = ( 0. ` K ) $.
    iscvlat2.a $e |- A = ( Atoms ` K ) $.
    $( The predicate "is an atomic lattice with the covering (or exchange)
       property". $)
    iscvlat2 $p |- ( K e. CvLat
        <-> ( K e. AtLat /\ A. p e. A A. q e. A A. x e. B
       ( ( ( p ./\ x ) = Z /\ p .<_ ( x .\/ q ) ) -> q .<_ ( x .\/ p ) ) ) ) $=
      ( wcel cv wa wral clc cal wbr wn co wi wceq iscvlat wb simpll simpr atnle
      simplrl syl3anc anbi1d imbi1d ralbidva 2ralbidva pm5.32i bitri ) EUAQEUBQ
      ZJRZARZFUCUDZVBVCIRZDUEFUCZSZVEVCVBDUEFUCZUFZACTZIBTJBTZSVAVBVCGUEHUGZVFS
      ZVHUFZACTZIBTJBTZSABCDEFIJKLMPUHVAVKVPVAVJVOJIBBVAVBBQZVEBQZSZSZVIVNACVTV
      CCQZSZVGVMVHWBVDVLVFWBVAVQWAVDVLUIVAVSWAUJVAVQVRWAUMVTWAUKBCVBEFGVCHKLNOP
      ULUNUOUPUQURUSUT $.
      $( [5-Nov-2012] $)
  $}

  ${
    $d p q x K $.
    $( An atomic lattice with the covering property is an atomic lattice. $)
    cvlatl $p |- ( K e. CvLat -> K e. AtLat ) $=
      ( vp vx vq clc wcel cal cv cple cfv wbr wn cjn co wa cbs wral catm eqid
      wi iscvlat simplbi ) AEFAGFBHZCHZAIJZKLUCUDDHZAMJZNUEKOUFUDUCUGNUEKTCAPJZ
      QDARJZQBUIQCUIUHUGAUEDBUHSUESUGSUISUAUB $.
      $( [5-Nov-2012] $)
  $}

  $( An atomic lattice with the covering property is a lattice. $)
  cvllat $p |- ( K e. CvLat -> K e. Lat ) $=
    ( clc wcel cal clat cvlatl atllat syl ) ABCADCAECAFAGH $.
    $( [5-Nov-2012] $)

  $( An atomic lattice with the covering property is a poset. $)
  cvlpos $p |- ( K e. CvLat -> K e. Poset ) $=
    ( clc wcel clat cpo cvllat latpos syl ) ABCADCAECAFAGH $.
    $( [5-Nov-2012] $)

  ${
    $d p q A $.  $d p q x B $.  $d p q x .\/ $.  $d p q x K $.  $d p q x .<_ $.
    $d p q x P $.  $d q x Q $.  $d x X $.
    cvlexch.b $e |- B = ( Base ` K ) $.
    cvlexch.l $e |- .<_ = ( le ` K ) $.
    cvlexch.j $e |- .\/ = ( join ` K ) $.
    cvlexch.a $e |- A = ( Atoms ` K ) $.
    $( An atomic covering lattice has the exchange property. $)
    cvlexch1 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
        /\ -. P .<_ X ) -> ( P .<_ ( X .\/ Q ) -> Q .<_ ( X .\/ P ) ) ) $=
      ( vp vx vq wcel wbr co wi wa clc w3a wn cv wral cal iscvlat simprbi breq1
      wceq notbid anbi12d oveq2 breq2d imbi12d anbi2d breq2 oveq1 rcla43v mpan9
      exp4b 3imp ) FUAPZCAPDAPHBPUBZCHGQZUCZCHDERZGQZDHCERZGQZSVCVDVFVHVJVCMUDZ
      NUDZGQZUCZVKVLOUDZERZGQZTZVOVLVKERZGQZSZNBUEOAUEMAUEZVDVFVHTZVJSZVCFUFPWB
      NABEFGOMIJKLUGUHWAWDCVLGQZUCZCVPGQZTZVOVLCERZGQZSWFCVLDERZGQZTZDWIGQZSMON
      CDHAABVKCUJZVRWHVTWJWOVNWFVQWGWOVMWEVKCVLGUIUKVKCVPGUIULWOVSWIVOGVKCVLEUM
      UNUOVODUJZWHWMWJWNWPWGWLWFWPVPWKCGVODVLEUMUNUPVODWIGUIUOVLHUJZWMWCWNVJWQW
      FVFWLVHWQWEVEVLHCGUQUKWQWKVGCGVLHDEURUNULWQWIVIDGVLHCEURUNUOUSUTVAVB $.
      $( [6-Nov-2011] $)

    $( An atomic covering lattice has the exchange property. $)
    cvlexch2 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
        /\ -. P .<_ X ) -> ( P .<_ ( Q .\/ X ) -> Q .<_ ( P .\/ X ) ) ) $=
      ( wcel w3a wbr co wceq atbase syl latjcom clc wn cvlexch1 cvllat 3ad2ant1
      clat simp22 simp23 syl3anc breq2d simp21 3imtr4d ) FUAMZCAMZDAMZHBMZNZCHG
      OUBZNZCHDEPZGODHCEPZGOCDHEPZGODCHEPZGOABCDEFGHIJKLUCUSVBUTCGUSFUFMZDBMZUP
      VBUTQUMUQVDURFUDUEZUSUOVEUMUNUOUPURUGABDFILRSUMUNUOUPURUHZBEFDHIKTUIUJUSV
      CVADGUSVDCBMZUPVCVAQVFUSUNVHUMUNUOUPURUKABCFILRSVGBEFCHIKTUIUJUL $.
      $( [6-May-2012] $)

    $( An atomic covering lattice has the exchange property. $)
    cvlexchb1 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
    /\ -. P .<_ X ) -> ( P .<_ ( X .\/ Q ) <-> ( X .\/ P ) = ( X .\/ Q ) ) ) $=
      ( wcel wbr wa wb adantr syl3anc 3adant3 mpbi2and clc w3a wceq clat cvllat
      simpr3 simpr1 atbase syl latjcl simpr2 latasymb latjle12 syl13anc latlej1
      wn co simpr cvlexch1 imp ex wi latlej2 breq2 syl5ibcom impbid ) FUAMZCAMZ
      DAMZHBMZUBZCHGNUPZUBZCHDEUQZGNZHCEUQZVNUCZVMVOVQVMVOOZVPVNGNZVNVPGNZVQVMV
      SVTOVQPZVOVGVKWAVLVGVKOZFUDMZVPBMZVNBMZWAVGWCVKFUEQZWBWCVJCBMZWDWFVGVHVIV
      JUFZWBVHWGVGVHVIVJUGABCFILUHUIZBEFHCIKUJRZWBWCVJDBMZWEWFWHWBVIWKVGVHVIVJU
      KABDFILUHUIZBEFHDIKUJRZBFGVPVNIJULRSQVRHVNGNZVOVSVMWNVOOVSPZVOVGVKWOVLWBW
      CVJWGWEWOWFWHWIWMBEFGHCVNIJKUMUNSQVMWNVOVGVKWNVLWBWCVJWKWNWFWHWLBEFGHDIJK
      UORSQVMVOURTVRHVPGNZDVPGNZVTVMWPWQOVTPZVOVGVKWRVLWBWCVJWKWDWRWFWHWLWJBEFG
      HDVPIJKUMUNSQVMWPVOVGVKWPVLWBWCVJWGWPWFWHWIBEFGHCIJKUORSQVMVOWQABCDEFGHIJ
      KLUSUTTTVAVGVKVQVOVBVLWBCVPGNZVQVOWBWCVJWGWSWFWHWIBEFGHCIJKVCRVPVNCGVDVES
      VF $.
      $( [16-Nov-2011] $)

    $( An atomic covering lattice has the exchange property. $)
    cvlexchb2 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
    /\ -. P .<_ X ) -> ( P .<_ ( Q .\/ X ) <-> ( P .\/ X ) = ( Q .\/ X ) ) ) $=
      ( wcel w3a wbr co wceq atbase syl latjcom wn cvlexchb1 clat cvllat simp22
      clc 3ad2ant1 simp23 syl3anc breq2d simp21 eqeq12d 3bitr4d ) FUFMZCAMZDAMZ
      HBMZNZCHGOUAZNZCHDEPZGOHCEPZVAQCDHEPZGOCHEPZVCQABCDEFGHIJKLUBUTVCVACGUTFU
      CMZDBMZUQVCVAQUNURVEUSFUDUGZUTUPVFUNUOUPUQUSUEABDFILRSUNUOUPUQUSUHZBEFDHI
      KTUIZUJUTVDVBVCVAUTVECBMZUQVDVBQVGUTUOVJUNUOUPUQUSUKABCFILRSVHBEFCHIKTUIV
      IULUM $.
      $( [22-Jun-2012] $)
  $}

  ${
    cvlexch3.b $e |- B = ( Base ` K ) $.
    cvlexch3.l $e |- .<_ = ( le ` K ) $.
    cvlexch3.j $e |- .\/ = ( join ` K ) $.
    cvlexch3.m $e |- ./\ = ( meet ` K ) $.
    cvlexch3.z $e |- Z = ( 0. ` K ) $.
    cvlexch3.a $e |- A = ( Atoms ` K ) $.
    $( An atomic covering lattice has the exchange property.  ( ~ atexch
       analog.) $)
    cvlexch3 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
        /\ ( P ./\ X ) = Z ) -> ( P .<_ ( X .\/ Q ) -> Q .<_ ( X .\/ P ) ) ) $=
      ( clc wcel co wbr w3a wceq wi wa wn cal cvlatl adantr simpr1 simpr3 atnle
      wb syl3anc cvlexch1 3expia sylbird 3impia ) FQRZCARZDARZIBRZUAZCIHSJUBZCI
      DESGTDICESGTUCZURVBUDZVCCIGTUEZVDVEFUFRZUSVAVFVCULURVGVBFUGUHURUSUTVAUIUR
      USUTVAUJABCFGHIJKLNOPUKUMURVBVFVDABCDEFGIKLMPUNUOUPUQ $.
      $( [5-Nov-2012] $)

    $( An atomic covering lattice has the exchange property.  Part of
       Definition 7.8 of [MaedaMaeda] p. 32. $)
    cvlexch4 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ X e. B )
        /\ ( P ./\ X ) = Z )
    -> ( P .<_ ( X .\/ Q ) <-> ( X .\/ P ) = ( X .\/ Q ) ) ) $=
      ( wcel co wceq wbr clc w3a wb wa wn cal cvlatl adantr simpr1 simpr3 atnle
      syl3anc cvlexchb1 3expia sylbird 3impia ) FUAQZCAQZDAQZIBQZUBZCIHRJSZCIDE
      RZGTICERVCSUCZUQVAUDZVBCIGTUEZVDVEFUFQZURUTVFVBUCUQVGVAFUGUHUQURUSUTUIUQU
      RUSUTUJABCFGHIJKLNOPUKULUQVAVFVDABCDEFGIKLMPUMUNUOUP $.
      $( [5-Nov-2012] $)
  $}

  ${
    cvlatexch.l $e |- .<_ = ( le ` K ) $.
    cvlatexch.j $e |- .\/ = ( join ` K ) $.
    cvlatexch.a $e |- A = ( Atoms ` K ) $.
    $( A version of ~ cvlexchb1 for atoms. $)
    cvlatexchb1 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ R e. A )
       /\ P =/= R ) -> ( P .<_ ( R .\/ Q ) <-> ( R .\/ P ) = ( R .\/ Q ) ) ) $=
      ( clc wcel w3a wne co wbr wceq wb wa wn cal cvlatl adantr syl3anc cbs cfv
      simpr1 simpr3 atncmp eqid atbase cvlexchb1 3expia syl3anr3 sylbird 3impia
      wi ) FKLZBALZCALZDALZMZBDNZBDCEOZGPDBEOVDQRZURVBSZVCBDGPTZVEVFFUALZUSVAVG
      VCRURVHVBFUBUCURUSUTVAUGURUSUTVAUHABDFGHJUIUDVAUSURUTDFUEUFZLZVGVEUQAVIDF
      VIUJZJUKURUSUTVJMVGVEAVIBCEFGDVKHIJULUMUNUOUP $.
      $( [5-Nov-2012] $)

    $( A version of ~ cvlexchb2 for atoms. $)
    cvlatexchb2 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ R e. A )
       /\ P =/= R ) -> ( P .<_ ( Q .\/ R ) <-> ( P .\/ R ) = ( Q .\/ R ) ) ) $=
      ( clc wcel w3a co wbr wceq atbase syl latjcom syl3anc wne cvlatexchb1 cbs
      clat cfv cvllat 3ad2ant1 simp22 eqid simp23 breq2d simp21 eqeq12d 3bitr4d
      ) FKLZBALZCALZDALZMZBDUAZMZBDCENZGODBENZVBPBCDENZGOBDENZVDPABCDEFGHIJUBVA
      VDVBBGVAFUDLZCFUCUEZLZDVGLZVDVBPUOUSVFUTFUFUGZVAUQVHUOUPUQURUTUHAVGCFVGUI
      ZJQRVAURVIUOUPUQURUTUJAVGDFVKJQRZVGEFCDVKISTZUKVAVEVCVDVBVAVFBVGLZVIVEVCP
      VJVAUPVNUOUPUQURUTULAVGBFVKJQRVLVGEFBDVKISTVMUMUN $.
      $( [5-Nov-2012] $)

    $( Atom exchange property. $)
    cvlatexch1 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ R e. A )
        /\ P =/= R ) -> ( P .<_ ( R .\/ Q ) -> Q .<_ ( R .\/ P ) ) ) $=
      ( clc wcel w3a wne co wbr wceq cvlatexchb1 atbase syl clat cbs cfv cvllat
      3ad2ant1 simp23 eqid simp22 latlej2 syl3anc breq2 syl5ibrcom sylbid ) FKL
      ZBALZCALZDALZMZBDNZMZBDCEOZGPDBEOZVAQZCVBGPZABCDEFGHIJRUTVDVCCVAGPZUTFUAL
      ZDFUBUCZLZCVGLZVEUNURVFUSFUDUEUTUQVHUNUOUPUQUSUFAVGDFVGUGZJSTUTUPVIUNUOUP
      UQUSUHAVGCFVJJSTVGEFGDCVJHIUIUJVBVACGUKULUM $.
      $( [5-Nov-2012] $)

    $( Atom exchange property. $)
    cvlatexch2 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ R e. A )
        /\ P =/= R ) -> ( P .<_ ( Q .\/ R ) -> Q .<_ ( P .\/ R ) ) ) $=
      ( wcel w3a co wbr wceq atbase syl latjcom syl3anc breq2d clc wne clat cbs
      cvlatexch1 cfv cvllat 3ad2ant1 simp22 eqid simp23 simp21 3imtr4d ) FUAKZB
      AKZCAKZDAKZLZBDUBZLZBDCEMZGNCDBEMZGNBCDEMZGNCBDEMZGNABCDEFGHIJUEUTVCVABGU
      TFUCKZCFUDUFZKZDVFKZVCVAOUNURVEUSFUGUHZUTUPVGUNUOUPUQUSUIAVFCFVFUJZJPQUTU
      QVHUNUOUPUQUSUKAVFDFVJJPQZVFEFCDVJIRSTUTVDVBCGUTVEBVFKZVHVDVBOVIUTUOVLUNU
      OUPUQUSULAVFBFVJJPQVKVFEFBDVJIRSTUM $.
      $( [5-Nov-2012] $)

    $( Atom exchange property. $)
    cvlatexch3 $p |- ( ( K e. CvLat /\ ( P e. A /\ Q e. A /\ R e. A )
        /\ ( P =/= Q /\ P =/= R ) )
             -> ( P .<_ ( Q .\/ R ) -> ( P .\/ Q ) = ( P .\/ R ) ) ) $=
      ( wcel w3a wne wa co wceq wb biimpa syl atbase simp1 simp21 simp23 simp22
      clc wbr simp3l cvlatexchb1 syl131anc clat cbs simpl1 simpl21 eqid simpl22
      cfv cvllat latjcom syl3anc cvlatexchb2 3adant3l 3eqtr4d ex ) FUEKZBAKZCAK
      ZDAKZLZBCMZBDMZNZLZBCDEOZGUFZBCEOZBDEOZPVLVNNZCBEOZVMVOVPVLVNVRVMPZVLVDVE
      VGVFVIVNVSQVDVHVKUAVDVEVFVGVKUBVDVEVFVGVKUCVDVEVFVGVKUDVDVHVIVJUGABDCEFGH
      IJUHUIRVQFUJKZBFUKUPZKZCWAKZVOVRPVQVDVTVDVHVKVNULFUQSVQVEWBVEVFVGVDVKVNUM
      AWABFWAUNZJTSVQVFWCVEVFVGVDVKVNUOAWACFWDJTSWAEFBCWDIURUSVLVNVPVMPZVDVHVJV
      NWEQVIABCDEFGHIJUTVARVBVC $.
      $( [29-Nov-2012] $)
  $}

  ${
    $d q z A $.  $d q z B $.  $d q z .\/ $.  $d q z K $.  $d q z .<_ $.
    $d q z X $.  $d q z P $.
    cvlcvr1.b $e |- B = ( Base ` K ) $.
    cvlcvr1.l $e |- .<_ = ( le ` K ) $.
    cvlcvr1.j $e |- .\/ = ( join ` K ) $.
    cvlcvr1.c $e |- C = ( <o ` K ) $.
    cvlcvr1.a $e |- A = ( Atoms ` K ) $.
    $( The covering property.  Proposition 1(ii) in [Kalmbach] p. 140 (and its
       converse).  ( ~ chcv1 analog.) $)
    cvlcvr1 $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. CvLat )
             /\ X e. B /\ P e. A )
      -> ( -. P .<_ X <-> X C ( X .\/ P ) ) ) $=
      ( vz wcel wbr wa wi syl3anc adantr vq coml ccla clc w3a wn co cplt cfv cv
      wceq wral clat simp13 cvllat syl simp2 atbase 3ad2ant3 eqid latnle biimpd
      simpl13 simprll simpl2 simpl3 latjcl simprrr wrex simprrl simpl11 simpl12
      cal cvlatl atlrelat1 syl311anc mpd ad2antrl lattrd simprl simpll3 simpll2
      cvlexch1 syl131anc simprlr mpbid latjle12 syl13anc pltle mpbi2and eqbrtrd
      wb cvlexchb1 exp32 rexlimdv latasymd exp44 ralrimdva jcad cvrval2 sylibrd
      imp simpr cvrlt syl31anc ex impbid ) FUBOZFUCOZFUDOZUEZHBOZDAOZUEZDHGPUFZ
      HHDEUGZCPZXNXOHXPFUHUIZPZHNUJZXRPZXTXPGPZQZXTXPUKZRZNBULZQZXQXNXOXSYFXNXO
      XSXNFUMOZXLDBOZXOXSWLXNXJYHXHXIXJXLXMUNFUOZUPZXKXLXMUQZXMXKYIXLABDFIMURZU
      SZBXREFGHDIJXRUTZKVASZVBXNXOYENBXNXTBOZXOYERXNYQXOYCYDXNYQXOQZYCQZQZBFGXT
      XPIJYTXJYHXHXIXJXLXMYSVCZYJUPZXNYQXOYCVDZYTYHXLYIXPBOZUUBXKXLXMYSVEZYTXMY
      IXKXLXMYSVFYMUPBEFHDIKVGZSZXNYRYAYBVHZYTUAUJZHGPUFZUUIXTGPZQZUAAVIZXPXTGP
      ZYTYAUUMXNYRYAYBVJZYTXHXIFVMOZXLYQYAUUMRXHXIXJXLXMYSVKZXHXIXJXLXMYSVLYTXJ
      UUPUUAFVNUPUUEUUCABXRFGHXTUAIJYOMVOVPVQYTUULUUNUAAYTUUIAOZUULUUNYTUURUULQ
      ZQZXPHUUIEUGZXTGUUTDUVAGPZXPUVAUKZUUTUUIXPGPZUVBUUTBFGUUIXTXPIJYTYHUUSUUB
      TZUURUUIBOZYTUULABUUIFIMURVRZYTYQUUSUUCTZYTUUDUUSUUGTYTUURUUJUUKVHZYTYBUU
      SUUHTVSUUTXJUURXMXLUUJUVDUVBRYTXJUUSUUATZYTUURUULVTZXKXLXMYSUUSWAZXKXLXMY
      SUUSWBZYTUURUUJUUKVJABUUIDEFGHIJKMWCWDVQUUTXJXMUURXLXOUVBUVCWLUVJUVLUVKUV
      MYTXOUUSXNYQXOYCWETABDUUIEFGHIJKMWMWDWFUUTHXTGPZUUKUVAXTGPZUUTYHXLUVFYQUV
      NUUKQUVOWLUVEUVMUVGUVHBEFGHUUIXTIJKWGWHYTUVNUUSYTYAUVNUUOYTXHXLYQYAUVNRUU
      QUUEUUCUBBBXRFGHXTJYOWISVQTUVIWJWKWNWOVQWPWQXBWRWSXNYHXLUUDXQYGWLYKYLXNYH
      XLYIUUDYKYLYNUUFSZNUMBCXRFGHXPIJYOLWTSXAXNXQXSXOXNXQXSXNXQQYHXLUUDXQXSXNY
      HXQYKTXKXLXMXQVEXNUUDXQUVPTXNXQXCUMBCXRFHXPIYOLXDXEXFYPXAXG $.
      $( [5-Nov-2012] $)
  $}

  ${
    cvlcvrp.b $e |- B = ( Base ` K ) $.
    cvlcvrp.j $e |- .\/ = ( join ` K ) $.
    cvlcvrp.m $e |- ./\ = ( meet ` K ) $.
    cvlcvrp.z $e |- Z = ( 0. ` K ) $.
    cvlcvrp.c $e |- C = ( <o ` K ) $.
    cvlcvrp.a $e |- A = ( Atoms ` K ) $.
    $( A Hilbert lattice satisfies the covering property of Definition 7.4 of
       [MaedaMaeda] p. 31 and its converse.  ( ~ cvp analog.) $)
    cvlcvrp $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. CvLat )
          /\ X e. B /\ P e. A ) -> ( ( X ./\ P ) = Z <-> X C ( X .\/ P ) ) ) $=
      ( wcel w3a co wceq wbr coml ccla clc cple cfv wn clat simp13 cvllat simp2
      syl atbase 3ad2ant3 latmcom syl3anc eqeq1d cal wb cvlatl simp3 eqid atnle
      cvlcvr1 3bitr2d ) FUAPZFUBPZFUCPZQZHBPZDAPZQZHDGRZISDHGRZISZDHFUDUEZTUFZH
      HDERCTVKVLVMIVKFUGPZVIDBPZVLVMSVKVGVQVEVFVGVIVJUHZFUIUKVHVIVJUJZVJVHVRVIA
      BDFJOULUMBFGHDJLUNUOUPVKFUQPZVJVIVPVNURVKVGWAVSFUSUKVHVIVJUTVTABDFVOGHIJV
      OVAZLMOVBUOABCDEFVOHJWBKNOVCVD $.
      $( [5-Nov-2012] $)
  $}

  ${
    cvlatcvr1.j $e |- .\/ = ( join ` K ) $.
    cvlatcvr1.c $e |- C = ( <o ` K ) $.
    cvlatcvr1.a $e |- A = ( Atoms ` K ) $.
    $( An atom is covered by its join with a different atom. $)
    cvlatcvr1 $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. CvLat )
            /\ P e. A /\ Q e. A ) -> ( P =/= Q <-> P C ( P .\/ Q ) ) ) $=
      ( coml wcel ccla clc w3a wne cmee cfv co wb eqid cp0 simp13 cvlatl atnem0
      wceq wbr cal syl syld3an1 cbs atbase cvlcvrp syl3an2 bitrd ) FJKZFLKZFMKZ
      NZCAKZDAKZNZCDOZCDFPQZRFUAQZUEZCCDERBUFZFUGKZUSURUTVBVESVAUQVGUOUPUQUSUTU
      BFUCUHACDFVCVDVCTZVDTZIUDUIUSURCFUJQZKUTVEVFSAVJCFVJTZIUKAVJBDEFVCCVDVKGV
      HVIHIULUMUN $.
      $( [5-Nov-2012] $)

    $( An atom is covered by its join with a different atom. $)
    cvlatcvr2 $p |- ( ( ( K e. OML /\ K e. CLat /\ K e. CvLat )
       /\ P e. A /\ Q e. A ) -> ( P =/= Q <-> P C ( Q .\/ P ) ) ) $=
      ( coml wcel ccla clc w3a wne co wbr cvlatcvr1 clat atbase cbs wceq simp13
      cfv cvllat syl eqid 3ad2ant2 3ad2ant3 latjcom syl3anc breq2d bitrd ) FJKZ
      FLKZFMKZNZCAKZDAKZNZCDOCCDEPZBQCDCEPZBQABCDEFGHIRUTVAVBCBUTFSKZCFUAUDZKZD
      VDKZVAVBUBUTUPVCUNUOUPURUSUCFUEUFURUQVEUSAVDCFVDUGZITUHUSUQVFURAVDDFVGITU
      IVDEFCDVGGUJUKULUM $.
      $( [5-Nov-2012] $)
  $}

  ${
    cvlsupr2.a $e |- A = ( Atoms ` K ) $.
    cvlsupr2.l $e |- .<_ = ( le ` K ) $.
    cvlsupr2.j $e |- .\/ = ( join ` K ) $.
    $( Two equivalent ways of expressing that ` R ` is a superposition of ` P `
       and ` Q ` . $)
    cvlsupr2 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A ) /\ P =/= Q )
      -> ( ( P .\/ R ) = ( Q .\/ R )
            <-> ( R =/= P /\ R =/= Q /\ R .<_ ( P .\/ Q ) ) ) ) $=
      ( wcel wne co wceq wbr wa wb oveq2 syl syl3anc simpl3 necomd simplr eqcom
      clc w3a eqeq12d syl6bb adantl mpbid clat cbs simpl1 cvllat simpl21 atbase
      cfv eqid latjidm syl2anc adantr eqtrd simpl22 latleeqj1 cal cvlatl bitr3d
      ex atcmp sylibd necon3d mpd simpl23 latlej1 breqtrrd cvlatexch1 syl131anc
      simpr 3jca simpr3 latjcom breq2d simpr2 simpr1 cvlatexchb2 sylbid impbida
      wi ) FUEKZBAKZCAKZDAKZUFZBCLZUFZBDEMZCDEMZNZDBLZDCLZDBCEMZGOZUFZWOWRPZWSW
      TXBXDCBLZWSXDBCWIWMWNWRUAZUBZXDDBCBXDDBNZCBEMZBNZCBNZXDXHXJXDXHPZXIBBEMZB
      XLWRXIXMNZWOWRXHUCXHWRXNQXDXHWRXMXINXNXHWPXMWQXIDBBERDBCERUGXMXIUDUHUIUJX
      DXMBNZXHXDFUKKZBFULUQZKZXOXDWIXPWIWMWNWRUMZFUNZSZXDWJXRWJWKWLWIWNWRUOZAXQ
      BFXQURZHUPZSZXQEFBYCJUSUTVAVBVHXDCBGOZXJXKXDXPCXQKZXRYFXJQYAXDWKYGWJWKWLW
      IWNWRVCZAXQCFYCHUPZSZYEXQEFGCBYCIJVDTXDFVEKZWKWJYFXKQXDWIYKXSFVFSZYHYBACB
      FGIHVITVGVJVKVLXDWNWTXFXDDCBCXDDCNZXACNZBCNZXDYMYNXDYMPZXACCEMZCYPWRXAYQN
      ZWOWRYMUCYMWRYRQXDYMWPXAWQYQDCBERDCCERUGUIUJXDYQCNZYMXDXPYGYSYAYJXQEFCYCJ
      USUTVAVBVHXDBCGOZYNYOXDXPXRYGYTYNQYAYEYJXQEFGBCYCIJVDTXDYKWJWKYTYOQYLYBYH
      ABCFGIHVITVGVJVKVLXDCWPGOZXBXDCWQWPGXDXPYGDXQKZCWQGOYAYJXDWLUUBWJWKWLWIWN
      WRVMZAXQDFYCHUPSXQEFGCDYCIJVNTWOWRVRVOXDWIWKWLWJXEUUAXBWHXSYHUUCYBXGACDBE
      FGIJHVPVQVLVSWOXCPZXBWRWOWSWTXBVTUUDXBDXIGOZWRUUDXAXIDGUUDXPXRYGXAXINUUDW
      IXPWIWMWNXCUMZXTSUUDWJXRWJWKWLWIWNXCUOZYDSUUDWKYGWJWKWLWIWNXCVCZYISXQEFBC
      YCJWATWBUUDUUEBWQGOZWRUUDWIWLWJWKWTUUEUUIWHUUFWJWKWLWIWNXCVMZUUGUUHWOWSWT
      XBWCADBCEFGIJHVPVQUUDWIWJWKWLBDLUUIWRQUUFUUGUUHUUJUUDDBWOWSWTXBWDUBABCDEF
      GIJHWEVQVJWFVLWG $.
      $( [5-Nov-2012] $)

    $( Two equivalent ways of expressing that ` R ` is a superposition of ` P `
       and ` Q ` , which can replace the superposition part of ~ ishlat1 ,
       ` ( x =/= y -> E. z e. A ( z =/= x /\ z =/= y /\ z .<_ ( x .\/ y ) ) `
       ` ) ` , with the simpler ` E. z e. A ( x .\/ z ) = ( y .\/ z ) ` as
       shown in ~ ishlat3 . $)
    cvlsupr3 $p |- ( ( K e. CvLat
          /\ ( P e. A /\ Q e. A /\ R e. A ) )
      -> ( ( P .\/ R ) = ( Q .\/ R )
          <-> ( P =/= Q -> ( R =/= P /\ R =/= Q /\ R .<_ ( P .\/ Q ) ) ) ) ) $=
      ( co wceq wne wi clc wcel w3a wa wbr wn df-ne imbi1i oveq1 pm4.83 3bitrri
      biantrur wb cvlsupr2 3expia pm5.74d syl5bb ) BDEKCDEKLZBCMZULNZFOPZBAPCAP
      DAPQZRZUMDBMDCMDBCEKGSQZNUNBCLZTZULNZUSULNZVARULUMUTULBCUAUBVBVABCDEUCUFU
      SULUDUEUQUMULURUOUPUMULURUGABCDEFGHIJUHUIUJUK $.
      $( [5-Nov-2012] $)

    $( Consequence of superposition condition
       ` ( P .\/ R ) = ( Q .\/ R ) ` . $)
    cvlsupr4 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A )
        /\ ( P =/= Q /\ ( P .\/ R ) = ( Q .\/ R ) ) ) -> R .<_ ( P .\/ Q ) ) $=
      ( clc wcel w3a wne co wceq wa wbr wi cvlsupr2 simp3 syl6bi 3exp imp4a
      3imp ) FKLZBALCALDALMZBCNZBDEOCDEOPZQDBCEOGRZUFUGUHUIUJUFUGUHUIUJSUFUGUHM
      UIDBNZDCNZUJMUJABCDEFGHIJTUKULUJUAUBUCUDUE $.
      $( [9-Nov-2012] $)
  $}

  ${
    cvlsupr5.a $e |- A = ( Atoms ` K ) $.
    cvlsupr5.j $e |- .\/ = ( join ` K ) $.
    $( Consequence of superposition condition
       ` ( P .\/ R ) = ( Q .\/ R ) ` . $)
    cvlsupr5 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A )
              /\ ( P =/= Q /\ ( P .\/ R ) = ( Q .\/ R ) ) ) -> R =/= P ) $=
      ( clc wcel w3a wne co wceq wa wi cple cfv wbr eqid simp1 syl6bi 3exp 3imp
      cvlsupr2 imp4a ) FIJZBAJCAJDAJKZBCLZBDEMCDEMNZODBLZUGUHUIUJUKUGUHUIUJUKPU
      GUHUIKUJUKDCLZDBCEMFQRZSZKUKABCDEFUMGUMTHUEUKULUNUAUBUCUFUD $.
      $( [9-Nov-2012] $)

    $( Consequence of superposition condition
       ` ( P .\/ R ) = ( Q .\/ R ) ` . $)
    cvlsupr6 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A )
              /\ ( P =/= Q /\ ( P .\/ R ) = ( Q .\/ R ) ) ) -> R =/= Q ) $=
      ( clc wcel w3a wne co wceq wa wi cple cfv wbr eqid simp2 syl6bi 3exp 3imp
      cvlsupr2 imp4a ) FIJZBAJCAJDAJKZBCLZBDEMCDEMNZODCLZUGUHUIUJUKUGUHUIUJUKPU
      GUHUIKUJDBLZUKDBCEMFQRZSZKUKABCDEFUMGUMTHUEULUKUNUAUBUCUFUD $.
      $( [9-Nov-2012] $)

    $( Consequence of superposition condition
       ` ( P .\/ R ) = ( Q .\/ R ) ` . $)
    cvlsupr7 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A )
              /\ ( P =/= Q /\ ( P .\/ R ) = ( Q .\/ R ) ) )
                -> ( P .\/ Q ) = ( R .\/ Q ) ) $=
      ( clc wcel w3a co wceq cfv wbr eqid atbase syl syl3anc breqtrd wne wa cbs
      cple cvllat 3ad2ant1 simp21 simp23 latlej1 simp3r simp22 latjcom wb simp1
      clat simp3l cvlatexchb2 syl131anc mpbid ) FIJZBAJZCAJZDAJZKZBCUAZBDELZCDE
      LZMZUBZKZBDCELZFUDNZOZBCELVKMZVJBVGVKVLVJBVFVGVLVJFUOJZBFUCNZJZDVPJZBVFVL
      OUTVDVOVIFUEUFZVJVAVQUTVAVBVCVIUGZAVPBFVPPZGQRVJVCVRUTVAVBVCVIUHZAVPDFWAG
      QRZVPEFVLBDWAVLPZHUISUTVDVEVHUJTVJVOCVPJZVRVGVKMVSVJVBWEUTVAVBVCVIUKZAVPC
      FWAGQRWCVPEFCDWAHULSTVJUTVAVCVBVEVMVNUMUTVDVIUNVTWBWFUTVDVEVHUPABDCEFVLWD
      HGUQURUS $.
      $( [24-Nov-2012] $)

    $( Consequence of superposition condition
       ` ( P .\/ R ) = ( Q .\/ R ) ` . $)
    cvlsupr8 $p |- ( ( K e. CvLat
              /\ ( P e. A /\ Q e. A /\ R e. A )
              /\ ( P =/= Q /\ ( P .\/ R ) = ( Q .\/ R ) ) )
                -> ( P .\/ Q ) = ( P .\/ R ) ) $=
      ( clc wcel w3a wne co wceq wa clat cbs cfv atbase syl cvllat eqid latjcom
      3ad2ant1 simp22 simp23 syl3anc simp3r cvlsupr7 3eqtr4rd ) FIJZBAJZCAJZDAJ
      ZKZBCLZBDEMZCDEMZNZOZKZURDCEMZUQBCEMVAFPJZCFQRZJZDVDJZURVBNUKUOVCUTFUAUDV
      AUMVEUKULUMUNUTUEAVDCFVDUBZGSTVAUNVFUKULUMUNUTUFAVDDFVGGSTVDEFCDVGHUCUGUK
      UOUPUSUHABCDEFGHUIUJ $.
      $( [24-Nov-2012] $)
  $}

