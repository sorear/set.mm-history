$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
     Construction of involution and inner product from a Hilbert lattice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c fRay $.

  $( Extend class notation with one-dimensional subspace (ray) of the
     functionals generated by a functional, i.e. the span of the singleton of a
     functional in the dual vector space. $)
  cfr $a class fRay $.

  ${
    $d w f g k $.
    $( Def. 8.6.49, p. 245. $)
    $( Define the span (in the dual of a vector space) of the singleton of a
       functional, expressed directly.  The result is either an atom (ray) of
       the dual space or the zero subspace (in the case of the zero
       functional) . $)
    df-fray $a |- fRay = ( w e. _V |->
          ( f e. ( LFnl ` w ) |-> { g e. ( LFnl ` w )
         | ( ( LKer ` w ) ` f ) C_ ( ( LKer ` w ) ` g ) } ) ) $.
  $}

  ${
    $d f g w F $.  $d w K $.  $d f g w W $.
    frayval.f $e |- F = ( LFnl ` W ) $.
    frayval.k $e |- K = ( LKer ` W ) $.
    frayval.r $e |- R = ( fRay ` W ) $.
    $( The ray generated by a functional, i.e. the span in the dual of a vector
       space of the singleton of a functional. $)
    frayfval $p |- ( W e. V -> R = ( f e. F |-> { g e. F
         | ( K ` f ) C_ ( K ` g ) } ) ) $=
      ( vw wcel cvv cv cfv wss crab cmpt clfn clk wceq cfr fveq2 syl6eqr fveq1d
      elex sseq12d rabeqbidv mpteq12dv df-fray fvex eqeltri mptex fvmpt syl5eq
      syl ) GFLGMLZABDBNZEOZCNZEOZPZCDQZRZUAGFUFUQAGUBOVDJKGBKNZSOZURVETOZOZUTV
      GOZPZCVFQZRVDMUBVEGUAZBVFVKDVCVLVFGSOZDVEGSUCHUDZVLVJVBCVFDVNVLVHUSVIVAVL
      URVGEVLVGGTOEVEGTUCIUDZUEVLUTVGEVOUEUGUHUIKBCUJBDVCDVMMHGSUKULUMUNUOUP $.
      $( [7-Oct-1014] $)

    $d f g G $.  $d f K $.
    $( The ray generated by a functional, i.e. the span in the dual of a vector
       space of the singleton of a functional. $)
    frayval $p |- ( ( W e. V /\ G e. F ) -> ( R ` G )
        = { g e. F | ( K ` G ) C_ ( K ` g ) } ) $=
      ( vf wcel wa cfv cv wss crab wceq cvv clfn frayfval adantr fveq1d eqeltri
      cmpt simpr fvex rabex fveq2 sseq1d rabbidv eqid fvmptg sylancl eqtrd ) GF
      LZDCLZMZDANDKCKOZENZBOENZPZBCQZUEZNZDENZVAPZBCQZURDAVDUPAVDRUQAKBCEFGHIJU
      AUBUCURUQVHSLVEVHRUPUQUFVGBCCGTNSHGTUGUDUHKDVCVHCSVDUSDRZVBVGBCVIUTVFVAUS
      DEUIUJUKVDULUMUNUO $.
      $( [7-Oct-1014] $)

    frayval.w $e |- ( ph -> W e. V ) $.
    frayval.g $e |- ( ph -> G e. F ) $.
    $( The ray generated by a functional, i.e. the span in the dual of a vector
       space of the singleton of a functional. $)
    frayvald $p |- ( ph -> ( R ` G ) = { g e. F | ( K ` G ) C_ ( K ` g ) } ) $=
      ( wcel cfv cv wss crab wceq frayval syl2anc ) AHGNEDNEBOEFOCPFOQCDRSLMBCD
      EFGHIJKTUA $.
      $( [7-Oct-1014] $)
  $}

  ${
    $d g ph $.  $d k D $.  $d g k F $.  $d g k G $.  $d k K $.  $d k T $.
    $d k V $.  $d g k W $.  $d k ph $.
    frayval2d.v $e |- V = ( Base ` W ) $.
    frayval2d.d $e |- D = ( Scalar ` W ) $.
    frayval2d.f $e |- F = ( LFnl ` W ) $.
    frayval2d.k $e |- K = ( Base ` D ) $.
    frayval2d.t $e |- T = ( .r ` D ) $.
    frayval2d.r $e |- R = ( fRay ` W ) $.
    frayval2d.w $e |- ( ph -> W e. LVec ) $.
    frayval2d.g $e |- ( ph -> G e. F ) $.
    $( The ray generated by a functional, i.e. the span in the dual of a vector
       space of the singleton of a functional. $)
    frayval2d $p |- ( ph -> ( R ` G )
        = { g e. F | E. k e. K g = ( G oF T ( V X. { k } ) ) } ) $=
      ( wcel cfv clk cv wss crab csn cxp co wceq wrex clvec eqid frayvald wa wi
      cof c0g clmod lveclmod lmod0cl ad2antrr simpr lfl0sc eqtr4d xpeq2d oveq2d
      syl sneq eqeq2d rcla4ev syl2anc a1d ad3antrrr simpllr lkrlss lssss adantr
      clss wb lkr0f biimpar sseq1d biimpa eqssd mpbid ex wne clsh simprr lkrshp
      syl3anc simplr simprl lshpcmp eqlkr2 syl121anc sylbid pm2.61da2ne lkrscss
      fveq2 sseq2d biimprcd syl6 rexlimdv impbid rabbidva eqtrd ) AHCUAHKUBUAZU
      AZEUCZXHUAZUDZEGUEXJHJFUCZUFZUGZDUPZUHZUIZFIUJZEGUEACEGHXHUKKNXHULZQRSUMA
      XLXSEGAXJGTZUNZXLXSYBXLXSUOXJJBUQUAZUFZUGZHYEYBXJYEUIZUNZXSXLYGYCITZXJHYE
      XPUHZUIZXSAYHYAYFAKURTZYHAKUKTZYKRKUSVGZBIKYCMOYCULZUTVGZVAYGXJYEYIYBYFVB
      YGBDGHIYCJKLMNOPYNAYKYAYFYMVAAHGTZYAYFSVAVCVDXRYJFYCIXMYCUIZXQYIXJYQXOYEH
      XPYQXNYDJXMYCVHVEVFVIVJZVKVLYBHYEUIZUNZXLXSYTXLUNZYHYJXSAYHYAYSXLYOVMUUAX
      JYEYIUUAXKJUIZYFUUAXKJUUAYLXKKVRUAZTZXKJUDAYLYAYSXLRVMUUAYKYAUUDAYKYAYSXL
      YMVMZAYAYSXLVNZUUCGXJXHKNXTUUCULZVOVKUUCXKJKUKLUUGVPVKYTXLJXKUDYTXIJXKYBX
      IJUIZYSYBYKYPUUHYSVSAYKYAYMVQAYPYASVQBGHXHYCJKMYNLNXTVTVKWAWBWCWDUUAYKYAU
      UBYFVSUUEUUFBGXJXHYCJKMYNLNXTVTVKWEUUABDGHIYCJKLMNOPYNUUEAYPYAYSXLSVMVCVD
      YRVKWFYBXJYEWGZHYEWGZUNZUNZXLXIXKUIZXSUULXIXKKWHUAZKUUNULZAYLYAUUKRVAZUUL
      YLYPUUJXIUUNTUUPAYPYAUUKSVAYBUUIUUJWIBGHUUNXHJKYCLMYNUUONXTWJWKUULYLYAUUI
      XKUUNTUUPAYAUUKWLYBUUIUUJWMBGXJUUNXHJKYCLMYNUUONXTWJWKWNUULUUMXSUULUUMUNY
      LYPYAUUMXSAYLYAUUKUUMRVMAYPYAUUKUUMSVMAYAUUKUUMVNUULUUMVBBDGHXJIXHJKFMOPL
      NXTWOWPWFWQWRYBXRXLFIYBXMITZXIXQXHUAZUDZXRXLUOYBUUQUUSYBUUQUNBXMDGHIXHJKL
      MOPNXTAYLYAUUQRVAAYPYAUUQSVAYBUUQVBWSWFXRXLUUSXRXKUURXIXJXQXHWTXAXBXCXDXE
      XFXG $.
      $( [9-Oct-1014] $)
  $}

  $c mapd $.

  $( Extend class notation with subspace map. $)
  cmpd $a class mapd $.

  ${
    $d s k w f v $.
    $( Def. 8.6.49, p. 245. $)
    $( Define a mapping from subspaces (or more generally subsets) of the
       vector space generated by isomorphism H, to subspaces of functionals in
       the dual vector space.  We use isomophism H to provide us with a
       projective polarity i.e. orthocomplent ` ocH ` , but any other polarity
       function could be used that satisfies Definition 3.2 in [Holland95]
       p. 214.  The ` C_ ` relation (rather than ` = ` ) causes the zero
       functional to be included, since its kernel is the full vector space. $)
    df-mapd $a |- mapd = ( k e. _V |-> ( w e. ( LHyp ` k ) |->
          ( s e. ~P ( Base ` ( ( DVecH ` k ) ` w ) ) |->
     ( ( LSpan ` ( LDual ` ( ( DVecH ` k ) ` w ) ) ) ` U_ v e. s
      { f e. ( LFnl ` ( ( DVecH ` k ) ` w ) ) | ( ( ( ocH ` k ) ` w ) ` { v } )
          = ( ( LKer ` ( ( DVecH ` k ) ` w ) ) ` f ) } ) ) ) ) $.
  $}

  ${
    $d k w H $.  $d f k s v w K $.
    mapdval.h $e |- H = ( LHyp ` K ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    mapdffval $p |- ( K e. X -> ( mapd ` K ) = ( w e. H |->
     ( s e. ~P ( Base ` ( ( DVecH ` K ) ` w ) ) |->
       ( ( LSpan ` ( LDual ` ( ( DVecH ` K ) ` w ) ) ) ` U_ v e. s
      { f e. ( LFnl ` ( ( DVecH ` K ) ` w ) ) | ( ( ( ocH ` K ) ` w ) ` { v } )
          = ( ( LKer ` ( ( DVecH ` K ) ` w ) ) ` f ) } ) ) ) ) $=
      ( vk cvv cfv cv cdvh cbs coch wceq cmpt clh fveq1d fveq2d cpw csn clk cld
      wcel cmpd clfn crab ciun clspn elex fveq2 syl6eqr pweqd eqeq12d rabeqbidv
      wel adantr iuneq2dv fveq12d mpteq12dv df-mapd fvex eqeltri mptex fvmpt
      syl ) EFUEEJUEEUFKADGALZEMKZKZNKZUAZBGLZBLUBZVHEOKZKZKZCLZVJUCKZKZPZCVJUG
      KZUHZUIZVJUDKZUJKZKZQZQZPEFUKIEAILZRKZGVHWJMKZKZNKZUAZBVMVNVHWJOKZKZKZVRW
      MUCKZKZPZCWMUGKZUHZUIZWMUDKZUJKZKZQZQWIJUFWJEPZAWKXHDWHXIWKERKZDWJERULHUM
      XIGWOXGVLWGXIWNVKXIWMVJNXIVHWLVIWJEMULSZTUNXIXDWDXFWFXIXEWEUJXIWMVJUDXKTT
      XIBVMXCWCXIXCWCPBGUQXIXAWACXBWBXIWMVJUGXKTXIWRVQWTVTXIVNWQVPXIVHWPVOWJEOU
      LSSXIVRWSVSXIWMVJUCXKTSUOUPURUSUTVAVAABCIGVBADWHDXJJHERVCVDVEVFVG $.
      $( [1-Nov-1014] $)

    $d f w F $.  $d w L $.  $d w N $.  $d w O $.  $d s w V $.  $d f s v w W $.
    mapdval.o $e |- O = ( ( ocH ` K ) ` W ) $.
    mapdval.m $e |- M = ( ( mapd ` K ) ` W ) $.
    mapdval.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    mapdval.v $e |- V = ( Base ` U ) $.
    mapdval.f $e |- F = ( LFnl ` U ) $.
    mapdval.l $e |- L = ( LKer ` U ) $.
    mapdval.d $e |- D = ( LDual ` U ) $.
    mapdval.n $e |- N = ( LSpan ` D ) $.
    mapdval.k $e |- ( ph -> ( K e. X /\ W e. H ) ) $.
    $( Mapping from vector space H to its dual space. $)
    mapdfval $p |- ( ph -> M = ( s e. ~P V |-> ( N ` U_ v e. s
       { f e. F | ( O ` { v } ) = ( L ` f ) } ) ) ) $=
      ( vw wcel wa cpw csn cfv wceq crab ciun cmpt cdvh cbs coch clk clfn clspn
      cld cmpd mapdffval fveq1d syl5eq fveq2 syl6eqr fveq2d pweqd wel rabeqbidv
      cv eqeq12d adantr iuneq2dv fveq12d mpteq12dv eqid fvex eqeltri pwex mptex
      cvv fvmpt sylan9eq syl ) AHOUHZNGUHZUIJPMUJZBPVNZBVNUKZLULZEVNZIULZUMZEFU
      NZUOZKULZUPZUMUFWIWJJNUGGPUGVNZHUQULZULZURULZUJZBWLWMXBHUSULZULZULZWOXDUT
      ULZULZUMZEXDVAULZUNZUOZXDVCULZVBULZULZUPZUPZULZXAWIJNHVDULZULYASWINYBXTUG
      BEGHOPQVEVFVGUGNXSXAGXTXBNUMZPXFXRWKWTYCXEMYCXEDURULZMYCXDDURYCXDNXCULDXB
      NXCVHTVIZVJUAVIVKYCXOWSXQKYCXQCVBULKYCXPCVBYCXPDVCULCYCXDDVCYEVJUDVIVJUEV
      IYCBWLXNWRYCXNWRUMBPVLYCXLWQEXMFYCXMDVAULFYCXDDVAYEVJUBVIYCXIWNXKWPYCWMXH
      LYCXHNXGULLXBNXGVHRVIVFYCWOXJIYCXJDUTULIYCXDDUTYEVJUCVIVFVOVMVPVQVRVSXTVT
      PWKWTMMYDWEUADURWAWBWCWDWFWGWH $.
      $( [1-Nov-1014] $)

    $d s F $.  $d s L $.  $d s N $.  $d s O $.  $d v s S $.
    mapdval.s $e |- ( ph -> S C_ V ) $.
    $( Mapping from vector space H to its dual space. $)
    mapdval $p |- ( ph -> ( M ` S )
        = ( N ` U_ v e. S { f e. F | ( O ` { v } ) = ( L ` f ) } ) ) $=
      ( vs cfv cpw cv csn wceq crab ciun cmpt mapdfval fveq1d wcel cvv wss fvex
      cbs eqeltri elpw2 biimpri syl a1i iuneq1 fveq2d eqid fvmptg syl2anc eqtrd
      ) ADKUIDUHNUJZBUHUKZBUKULMUIFUKJUIUMFGUNZUOZLUIZUPZUIZBDVQUOZLUIZADKVTABC
      EFGHIJKLMNOPUHQRSTUAUBUCUDUEUFUQURADVOUSZWCUTUSZWAWCUMADNVAZWDUGWDWFDNNEV
      CUIUTUAEVCVBVDVEVFVGWEAWBLVBVHUHDVSWCVOUTVTVPDUMVRWBLBVPDVQVIVJVTVKVLVMVN
      $.
      $( [1-Nov-1014] $)

    $d v F $.  $d f S $.
    $( Mapping from vector space H to its dual space. $)
    mapdval2 $p |- ( ph -> ( M ` S )
        = ( N ` { f e. F | E. v e. S ( O ` { v } ) = ( L ` f ) } ) ) $=
      ( cfv cv csn wceq crab ciun wrex mapdval iunrab fveq2i syl6eq ) ADKUHBDBU
      IUJMUHFUIJUHUKZFGULUMZLUHUSBDUNFGULZLUHABCDEFGHIJKLMNOPQRSTUAUBUCUDUEUFUG
      UOUTVALUSBFDGUPUQUR $.
      $( [6-Nov-1014] $)
  $}

  $c mapdOLD $.

  $( Extend class notation with one-dimensional subspace map. $)
  cmpdOLD $a class mapdOLD $.

  ${
    $d s k w f $.
    $( Def. 8.6.49, p. 245. $)
    $( Define a mapping from subspaces of the vector space generated by
       isomorphism H, to subspaces of functionals in the dual vector space.
       The ` C_ ` relation (rather than ` = ` ) causes the zero functional to
       be included, since its kernel is the full vector space. $)
    df-mapdOLD $a |- mapdOLD = ( k e. _V |-> ( w e. ( LHyp ` k ) |->
          ( s e. ( LSubSp ` ( ( DVecH ` k ) ` w ) ) |->
  { f e. ( LFnl ` ( ( DVecH ` k ) ` w ) ) | ( ( ( ocH ` k ) ` w ) ` s )
        C_ ( ( LKer ` ( ( DVecH ` k ) ` w ) ) ` f ) } ) ) ) $.
  $}

  ${
    $d k w H $.  $d f k s w K $.
    mapdOLDval.h $e |- H = ( LHyp ` K ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    mapdOLDffval $p |- ( K e. V -> ( mapdOLD ` K ) = ( w e. H
  |-> ( s e. ( LSubSp ` ( ( DVecH ` K ) ` w ) )
      |-> { f e. ( LFnl ` ( ( DVecH ` K ) ` w ) ) | ( ( ( ocH ` K ) ` w ) ` s )
          C_ ( ( LKer ` ( ( DVecH ` K ) ` w ) ) ` f ) } ) ) ) $=
      ( vk cvv cfv cv cdvh clss coch clk clfn cmpt clh fveq2 fveq1d cmpdOLD wss
      wcel crab wceq elex syl6eqr fveq2d sseq12d rabeqbidv mpteq12dv df-mapdOLD
      fvex eqeltri mptex fvmpt syl ) DEUCDIUCDUAJACFAKZDLJZJZMJZFKZURDNJZJZJZBK
      ZUTOJZJZUBZBUTPJZUDZQZQZUEDEUFHDAHKZRJZFURVNLJZJZMJZVBURVNNJZJZJZVFVQOJZJ
      ZUBZBVQPJZUDZQZQVMIUAVNDUEZAVOWGCVLWHVODRJZCVNDRSGUGWHFVRWFVAVKWHVQUTMWHU
      RVPUSVNDLSTZUHWHWDVIBWEVJWHVQUTPWJUHWHWAVEWCVHWHVBVTVDWHURVSVCVNDNSTTWHVF
      WBVGWHVQUTOWJUHTUIUJUKUKABHFULACVLCWIIGDRUMUNUOUPUQ $.
      $( [28-Oct-1014] $)

    $d s w A $.  $d f w F $.  $d w L $.  $d w O $.  $d f s w W $.
    mapdOLDval.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    mapdOLDval.a $e |- A = ( LSubSp ` U ) $.
    mapdOLDval.f $e |- F = ( LFnl ` U ) $.
    mapdOLDval.l $e |- L = ( LKer ` U ) $.
    mapdOLDval.o $e |- O = ( ( ocH ` K ) ` W ) $.
    mapdOLDval.m $e |- M = ( ( mapdOLD ` K ) ` W ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    mapdOLDfval $p |- ( ( K e. V /\ W e. H )
     -> M = ( s e. A |-> { f e. F | ( O ` s ) C_ ( L ` f ) } ) ) $=
      ( cfv vw wcel cv cdvh clss coch clk clfn crab cmpdOLD mapdOLDffval fveq1d
      wss cmpt syl5eq wceq fveq2 syl6eqr fveq2d sseq12d rabeqbidv mpteq12dv cvv
      eqid fvex eqeltri mptex fvmpt sylan9eq ) FJUBZKEUBHKUAELUAUCZFUDTZTZUETZL
      UCZVKFUFTZTZTZCUCZVMUGTZTZUMZCVMUHTZUIZUNZUNZTZLAVOITZVSGTZUMZCDUIZUNZVJH
      KFUJTZTWGSVJKWMWFUACEFJLMUKULUOUAKWEWLEWFVKKUPZLVNWDAWKWNVNBUETZAWNVMBUEW
      NVMKVLTBVKKVLUQNURZUSOURWNWBWJCWCDWNWCBUHTDWNVMBUHWPUSPURWNVRWHWAWIWNVOVQ
      IWNVQKVPTIVKKVPUQRURULWNVSVTGWNVTBUGTGWNVMBUGWPUSQURULUTVAVBWFVDLAWKAWOVC
      OBUEVEVFVGVHVI $.
      $( [28-Oct-1014] $)

    $d s F $.  $d s L $.  $d s O $.  $d f s S $.
    mapdOLDval.k $e |- ( ph -> ( K e. V /\ W e. H ) ) $.
    mapdOLDval.s $e |- ( ph -> S e. A ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    mapdOLDval $p |- ( ph -> ( M ` S ) = { f e. F | ( O ` S ) C_ ( L ` f ) } )
        $=
      ( vs cfv cv wss crab cmpt wcel wa wceq mapdOLDfval syl fveq1d cvv eqeltri
      clfn fvex rabex a1i fveq2 sseq1d rabbidv eqid fvmptg syl2anc eqtrd ) ACJU
      DCUCBUCUEZKUDZEUEIUDZUFZEFUGZUHZUDZCKUDZVJUFZEFUGZACJVMAHLUIMGUIUJJVMUKUA
      BDEFGHIJKLMUCNOPQRSTULUMUNACBUIVQUOUIZVNVQUKUBVRAVPEFFDUQUDUOQDUQURUPUSUT
      UCCVLVQBUOVMVHCUKZVKVPEFVSVIVOVJVHCKVAVBVCVMVDVEVFVG $.
      $( [28-Oct-1014] $)
  $}

  $c map1dimOLD $.

  $( Extend class notation with one-dimensional subspace map. $)
  cmp1dOLD $a class map1dimOLD $.

  ${
    $d q k w f $.
    $( Def. 8.6.49, p. 245. $)
    $( Define a mapping from a one-dimensional subspace of the vector space
       generated by isomorphism H, to a one-dimensional subspace (ray) of
       functionals in the dual vector space.  The ` C_ ` relation (rather than
       ` = ` ) causes the zero functional to be included, since its kernel is
       the full vector space.  This function corresponds to a projectivity in
       Baer, _Linear Algebra and Projective Geometry_, p. 40.  (Ideally, this
       would be a pure vector space definition, but it seems we need the
       orthocomplement from the Hilbert lattice ` k ` .) $)
    df-map1dimOLD $a |- map1dimOLD = ( k e. _V |-> ( w e. ( LHyp ` k ) |->
          ( q e. ( LSAtoms ` ( ( DVecH ` k ) ` w ) ) |->
  { f e. ( LFnl ` ( ( DVecH ` k ) ` w ) ) | ( ( ( ocH ` k ) ` w ) ` q )
        C_ ( ( LKer ` ( ( DVecH ` k ) ` w ) ) ` f ) } ) ) ) $.
  $}

  ${
    $d k w H $.  $d f k q w K $.
    map1dimOLDval.h $e |- H = ( LHyp ` K ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    map1dimOLDffval $p |- ( K e. V -> ( map1dimOLD ` K ) = ( w e. H
  |-> ( q e. ( LSAtoms ` ( ( DVecH ` K ) ` w ) )
      |-> { f e. ( LFnl ` ( ( DVecH ` K ) ` w ) ) | ( ( ( ocH ` K ) ` w ) ` q )
          C_ ( ( LKer ` ( ( DVecH ` K ) ` w ) ) ` f ) } ) ) ) $=
      ( vk cvv cfv cv cdvh clsa coch clk clfn cmpt clh fveq2 fveq1d wss syl6eqr
      wcel crab wceq elex fveq2d sseq12d rabeqbidv mpteq12dv df-map1dimOLD fvex
      cmp1dOLD eqeltri mptex fvmpt syl ) DEUCDIUCDUMJACFAKZDLJZJZMJZFKZURDNJZJZ
      JZBKZUTOJZJZUAZBUTPJZUDZQZQZUEDEUFHDAHKZRJZFURVNLJZJZMJZVBURVNNJZJZJZVFVQ
      OJZJZUAZBVQPJZUDZQZQVMIUMVNDUEZAVOWGCVLWHVODRJZCVNDRSGUBWHFVRWFVAVKWHVQUT
      MWHURVPUSVNDLSTZUGWHWDVIBWEVJWHVQUTPWJUGWHWAVEWCVHWHVBVTVDWHURVSVCVNDNSTT
      WHVFWBVGWHVQUTOWJUGTUHUIUJUJABHFUKACVLCWIIGDRULUNUOUPUQ $.
      $( [6-Oct-1014] $)

    $d q w A $.  $d f w F $.  $d w L $.  $d w O $.  $d f q w W $.
    map1dimOLDval.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDval.a $e |- A = ( LSAtoms ` U ) $.
    map1dimOLDval.f $e |- F = ( LFnl ` U ) $.
    map1dimOLDval.l $e |- L = ( LKer ` U ) $.
    map1dimOLDval.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDval.m $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    map1dimOLDfval $p |- ( ( K e. V /\ W e. H )
     -> M = ( q e. A |-> { f e. F | ( O ` q ) C_ ( L ` f ) } ) ) $=
      ( cfv wcel cdvh clsa coch clk wss clfn crab cmpt cmp1dOLD map1dimOLDffval
      vw cv fveq1d syl5eq wceq fveq2 syl6eqr fveq2d sseq12d rabeqbidv mpteq12dv
      eqid cvv fvex eqeltri mptex fvmpt sylan9eq ) FJUAZKEUAHKULELULUMZFUBTZTZU
      CTZLUMZVKFUDTZTZTZCUMZVMUETZTZUFZCVMUGTZUHZUIZUIZTZLAVOITZVSGTZUFZCDUHZUI
      ZVJHKFUJTZTWGSVJKWMWFULCEFJLMUKUNUOULKWEWLEWFVKKUPZLVNWDAWKWNVNBUCTZAWNVM
      BUCWNVMKVLTBVKKVLUQNURZUSOURWNWBWJCWCDWNWCBUGTDWNVMBUGWPUSPURWNVRWHWAWIWN
      VOVQIWNVQKVPTIVKKVPUQRURUNWNVSVTGWNVTBUETGWNVMBUEWPUSQURUNUTVAVBWFVCLAWKA
      WOVDOBUCVEVFVGVHVI $.
      $( [6-Oct-1014] $)

    $d q F $.  $d q L $.  $d q O $.  $d f q Q $.
    map1dimOLDval.k $e |- ( ph -> ( K e. V /\ W e. H ) ) $.
    map1dimOLDval.q $e |- ( ph -> Q e. A ) $.
    $( Mapping from vector space H atoms to rays of functionals. $)
    map1dimOLDval $p |- ( ph
           -> ( M ` Q ) = { f e. F | ( O ` Q ) C_ ( L ` f ) } ) $=
      ( vq cfv cv wss crab cmpt wcel wa wceq map1dimOLDfval syl fveq1d cvv clfn
      fvex eqeltri rabex a1i fveq2 sseq1d rabbidv eqid fvmptg syl2anc eqtrd ) A
      CJUDCUCBUCUEZKUDZEUEIUDZUFZEFUGZUHZUDZCKUDZVJUFZEFUGZACJVMAHLUIMGUIUJJVMU
      KUABDEFGHIJKLMUCNOPQRSTULUMUNACBUIVQUOUIZVNVQUKUBVRAVPEFFDUPUDUOQDUPUQURU
      SUTUCCVLVQBUOVMVHCUKZVKVPEFVSVIVOVJVHCKVAVBVCVMVDVEVFVG $.
      $( [6-Oct-1014] $)
  $}

$(
  @{
    map1dimOLDfray.h @e |- H = ( LHyp ` K ) @.
    map1dimOLDfray.u @e |- U = ( ( DVecH ` K ) ` W ) @.
    map1dimOLDfray.a @e |- A = ( LSAtoms ` U ) @.
    map1dimOLDfray.m @e |- M = ( ( map1dimOLD ` K ) ` W ) @.
    map1dimOLDfray.r @e |- R = ( fRay ` U ) @.
    map1dimOLDfray.k @e |- ( ph -> ( K e. HL /\ W e. H ) ) @.
    map1dimOLDfray.q @e |- ( ph -> Q e. A ) @.
    @( The value of the D map is a ray of functionals. @)
    map1dimOLDfray @p |- ( ph -> ( M ` Q ) e. ran R ) @=
? @.
      @( [12-Oct-1014] @)
  @}

  @{
    map1dimOLDrnfray.h @e |- H = ( LHyp ` K ) @.
    map1dimOLDrnfray.u @e |- U = ( ( DVecH ` K ) ` W ) @.
    map1dimOLDrnfray.m @e |- M = ( ( map1dimOLD ` K ) ` W ) @.
    map1dimOLDrnfray.r @e |- R = ( fRay ` U ) @.
    map1dimOLDrnfray.k @e |- ( ph -> ( K e. HL /\ W e. H ) ) @.
    @( The value of the D map is a ray of functionals. @)
    map1dimOLDrnfray @p |- ( ph -> ran M C_ ran R ) @=
? @.
      @( [12-Oct-1014] @)
  @}
$)

  ${
    $d g K $.  $d g M $.  $d g P $.  $d g Q $.  $d g U $.  $d g W $.
    $d g ph $.
    map1dimOLD11.h $e |- H = ( LHyp ` K ) $.
    map1dimOLD11.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLD11.a $e |- A = ( LSAtoms ` U ) $.
    map1dimOLD11.m $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLD11.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLD11.p $e |- ( ph -> P e. A ) $.
    map1dimOLD11.q $e |- ( ph -> Q e. A ) $.
    $( The value of the D map is one-to-one. $)
    map1dimOLD11 $p |- ( ph -> ( ( M ` P ) = ( M ` Q ) <-> P = Q ) ) $=
      ( vg cfv wceq wcel coch wa cv clk clfn wrex clvec clsh dvhlvec syl adantr
      chlt eqid dochsatshp lshpkr syl2anc w3a cbs csca c0g csn cxp wne 3ad2ant1
      simp3 eqeltrd simp2 lkrshp3 mpbid simpl3 eqcomd eqimss wb crab wral simpr
      map1dimOLDval 3eqtr3d rabbi sylibr r19.21bi 3adant3 biimpar lshpcmp bitrd
      wss mpdan eqtr3d 3exp rexlimdv mpd cdih crn dih1dimat doch11 sylibd fveq2
      ex impbid1 ) ACHRZDHRZSZCDSZAXBCIGUARRZRZDXDRZSZXCAXBXGAXBUBZQUCZEUDRZRZX
      ESZQEUERZUFZXGXHEUGTZXEEUHRZTZXNAXOXBAGULTIFTUBZXONEFGIJKUIUJUKZAXQXBABCE
      FGXDIXPJKXDUMZLXPUMZNOUNUKZXEQXMXPXJEYAXMUMZXJUMZUOUPXHXLXGQXMXHXIXMTZXLX
      GXHYEXLUQZXKXEXFXHYEXLVEZYFXIEURRZEUSRZUTRZVAVBVCZXKXFSYFXKXPTZYKYFXKXEXP
      YGXHYEXQXLYBVDVFYFYIXMXIXPXJYJYHEYHUMYIUMYJUMYAYCYDXHYEXOXLXSVDZXHYEXLVGV
      HZVIYFYKUBZXFXKYOXEXKWFZXFXKSZYOXEXKSYPYOXKXEXHYEXLYKVJVKXEXKVLUJYOYPXFXK
      WFZYQYFYPYRVMZYKXHYEYSXLXHYSQXMXHYPQXMVNZYRQXMVNZSYSQXMVOXHWTXAYTUUAAXBVP
      AWTYTSXBABCEQXMFGXJHXDULIJKLYCYDXTMNOVQUKAXAUUASXBABDEQXMFGXJHXDULIJKLYCY
      DXTMNPVQUKVRYPYRQXMVSVTWAWBUKYOXFXKXPEYAYFXOYKYMUKYFXFXPTZYKXHYEUUBXLAUUB
      XBABDEFGXDIXPJKXTLYANPUNUKVDUKYFYLYKYNWCWDWEVIVKWGWHWIWJWKWRAFIGWLRRZGXDI
      CDJUUCUMZXTNAXRCBTCUUCWMZTNOBCEFUUCGIJKUUDLWNUPAXRDBTDUUETNPBDEFUUCGIJKUU
      DLWNUPWOWPCDHWQWS $.
      $( [12-Oct-1014] $)
  $}

  ${
    $d f D $.  $d f F $.  $d f G $.  $d f K $.  $d f N $.  $d f Q $.  $d f W $.
    $d f ph $.
    map1dimOLDval2.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDval2.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDval2.i $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDval2.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDval2.a $e |- A = ( LSAtoms ` U ) $.
    map1dimOLDval2.f $e |- F = ( LFnl ` U ) $.
    map1dimOLDval2.l $e |- L = ( LKer ` U ) $.
    map1dimOLDval2.d $e |- D = ( LDual ` U ) $.
    map1dimOLDval2.n $e |- N = ( LSpan ` D ) $.
    map1dimOLDval2.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDval2.q $e |- ( ph -> Q e. A ) $.
    map1dimOLDval2.g $e |- ( ph -> G e. F ) $.
    map1dimOLDval2.e $e |- ( ph -> ( L ` G ) = ( O ` Q ) ) $.
    $( Value of vector space mapping, given a functional ` G ` whose kernel is
       the orthocomplement of the mapped 1-dim subspace. $)
    map1dimOLDval2 $p |- ( ph -> ( M ` Q ) = ( N ` { G } ) ) $=
      ( vf cfv wss crab csn chlt map1dimOLDval sseq1d rabbidv eqcomd eqtrd wcel
      cv wa clvec dvhlvec syl ldual1dim ) ADKUIZGJUIZUHUTJUIZUJZUHFUKZGULLUIZAV
      FDMUIZVHUJZUHFUKZVJABDEUHFHIJKMUMNORSTUAPQUDUEUNAVJVNAVIVMUHFAVGVLVHUGUOU
      PUQURAVKVJACUHFGJLETUAUBUCAIUMUSNHUSVAEVBUSUDEHINORVCVDUFVEUQUR $.
      $( [24-Oct-1014] $)
  $}

  ${
    $d g T $.  $d g K $.  $d g M $.  $d g X $.  $d g U $.  $d g W $.
    $d g ph $.
    mapdOLDcl.h $e |- H = ( LHyp ` K ) $.
    mapdOLDcl.i $e |- M = ( ( mapdOLD ` K ) ` W ) $.
    mapdOLDcl.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    mapdOLDcl.s $e |- S = ( LSubSp ` U ) $.
    mapdOLDcl.d $e |- D = ( LDual ` U ) $.
    mapdOLDcl.t $e |- T = ( LSubSp ` D ) $.
    mapdOLDcl.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    mapdOLDcl.x $e |- ( ph -> X e. S ) $.
    $( The vector space mapping of a subspace in the constructed vector space
       is a subspace in the dual space. $)
$(
    mapdOLDcl $p |- ( ph -> ( M ` X ) e. T ) $=
      (  ) ? $.
$)
      $( [28-Oct-1014] $)
  $}

  ${
    $d g B $.  $d g K $.  $d g M $.  $d g Q $.  $d g U $.  $d g W $.
    $d g ph $.
    map1dimOLDcl.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDcl.i $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDcl.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDcl.a $e |- A = ( LSAtoms ` U ) $.
    map1dimOLDcl.d $e |- D = ( LDual ` U ) $.
    map1dimOLDcl.b $e |- B = ( LSAtoms ` D ) $.
    map1dimOLDcl.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDcl.q $e |- ( ph -> Q e. A ) $.
    $( The vector space mapping of an atom (1-dim subspace) in the constructed
       vector space is an atom in the dual space. $)
    map1dimOLDcl $p |- ( ph -> ( M ` Q ) e. B ) $=
      ( cfv wcel vg cv cbs csca c0g csn cxp wne coch clk wceq wa clfn wrex clsh
      eqid dochsatshp chlt clvec dvhlvec islshp2 mpbid w3a clspn 3ad2ant1 simp2
      wb 3syl simp3r eqcomd map1dimOLDval2 clmod dvhlmod syl lduallmod cvv cdvh
      eqeltri a1i ldualvbase eleqtrrd simp3l ldualv0 neeqtrrd lsatlspsn syl3anc
      fvex eqeltrd 3exp rexlimdv mpd ) AUAUBZFUCSZFUDSZUESZUFUGZUHZEJHUISSZSZWL
      FUJSZSZUKZULZUAFUMSZUNZEISZCTZAWSFUOSZTZXEABEFGHWRJXHKMWRUPZNXHUPZQRUQAHU
      RTJGTULZFUSTXIXEVGQFGHJKMUTWNWSUAXDXHWTWMFWOWMUPZWNUPZWOUPZXKXDUPZWTUPZVA
      VHVBAXCXGUAXDAWLXDTZXCXGAXRXCVCZXFWLUFDVDSZSZCXSBDEFXDWLGHWTIXTWRJKXJLMNX
      PXQOXTUPZAXRXLXCQVEAXREBTXCRVEAXRXCVFZXSWSXAAXRWQXBVIVJVKXSDVLTZWLDUCSZTW
      LDUESZUHYACTAXRYDXCADFOAXLFVLTQFGHJKMVMVNZVOVEXSWLXDYEYCAXRYEXDUKXCADXDYE
      FVPXPOYEUPZFVPTAFJHVQSZSVPMJYIWGVRVSVTVEWAXSWLWPYFAXRWQXBWBAXRYFWPUKXCADW
      NYFWMFWOXMXNXOOYFUPZYGWCVEWDCXTYEDWLYFYHYBYJPWEWFWHWIWJWK $.
      $( [24-Oct-1014] $)
  $}

  ${
    dochnel.h $e |- H = ( LHyp ` K ) $.
    dochnel.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochnel.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochnel.v $e |- V = ( Base ` U ) $.
    dochnel.z $e |- Z = ( 0g ` U ) $.
    dochnel.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochnel.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    $( A nonzero vector doesn't belong to the orthocomplement of its
       singleton. $)
    dochnel $p |- ( ph -> -. X e. ( O ` { X } ) ) $=
      ( cfv wcel syl syl2anc csn clspn wn wne cdif eldifsn simprbi wa wceq chlt
      clmod dvhlmod eldifi eqid lspsnid biantrurd cin elin clss wss snssd lspcl
      dochnoncon eleq2d elsncg bitrd syl5bbr necon3bbid mpbird dochocsp mtbid
      wb ) AHHUAZBUBQZQZEQZRZHVMEQZRAVQUCHIUDZAHFIUAZUEZRZVSPWBHFRZVSHFIUFUGSAV
      QHIAVQHVORZVQUHZHIUIZAWDVQABUKRZWCWDADUJRGCRUHZWGOBCDGJLULSZAWBWCPHFVTUMS
      ZVNFBHMVNUNZUOTUPWEHVOVPUQZRZAWFHVOVPURAWMHVTRZWFAWLVTHAWHVOBUSQZRZWLVTUI
      OAWGVMFUTWPWIAHFWJVAZWOVMVNFBMWOUNZWKVBTWOBCDEGVOIJLWRNKVCTVDAWBWNWFVLPHI
      WAVESVFVGVFVHVIAVPVRHABCDVNEFGVMJLKMWKOWQVJVDVK $.
      $( [27-Oct-1014] $)
  $}

  ${
    dochexmid.h $e |- H = ( LHyp ` K ) $.
    dochexmid.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochexmid.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochexmid.v $e |- V = ( Base ` U ) $.
    dochexmid.z $e |- Z = ( 0g ` U ) $.
    dochexmid.r $e |- N = ( LSpan ` U ) $.
    dochexmid.p $e |- P = ( LSSum ` U ) $.
    dochexmid.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochexmid.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    $( Special case of excluded middle for the singleton of a vector. $)
    dochexmid $p |- ( ph -> ( ( O ` { X } ) P ( N ` { X } ) ) = V ) $=
      ( csn cfv clsh eqid chlt wcel wa clvec dvhlvec cdif eldifi snssd dochocsp
      syl clsa eqid1 clmod dvhlmod eldifsn simprbi lsatlspsn syl3anc dochsatshp
      wne eqeltrrd dochnel lshpnelr ) ABJUAZGUBZCUCUBZFHCJOQRVJUDAEUEUFIDUFUGZC
      UHUFSCDEILNUIUNAVHFUBZGUBVIVJACDEFGHIVHLNMOQSAJHAJHKUAZUJUFZJHUFZTJHVMUKU
      NZULUMACUOUBZVLCDEGIVJLNMVQUDZVJUPSACUQUFZVOJKVDZVLVQUFAVKVSSCDEILNURUNVP
      AVNVTTVNVOVTJHKUSUTUNVQFHCJKOQPVRVAVBVCVEVPACDEGHIJKLMNOPSTVFVG $.
      $( [27-Oct-1014] $)
  $}

  ${
    $d k v w A $.  $d k w D $.  $d k v w O $.  $d k v w R $.  $d k v w T $.
    $d k w U $.  $d k v w V $.  $d k v w X $.  $d k v w ph $.
    map1dimOLDlem1.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDlem1.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDlem1.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDlem1.v $e |- V = ( Base ` U ) $.
    map1dimOLDlem1.z $e |- Z = ( 0g ` U ) $.
    map1dimOLDlem1.a $e |- A = ( +g ` U ) $.
    map1dimOLDlem1.t $e |- T = ( vsca ` U ) $.
    map1dimOLDlem1.f $e |- F = ( LFnl ` U ) $.
    map1dimOLDlem1.d $e |- D = ( Scalar ` U ) $.
    map1dimOLDlem1.r $e |- R = ( Base ` D ) $.
    map1dimOLDlem1.g $e |- G = ( v e. V |->
        ( iota_ k e. R E. w e. ( O ` { X } ) v = ( w A ( k T X ) ) ) ) $.
    map1dimOLDlem1.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDlem1.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    $( Closure of a "canonical" functional ` G ` determined by a nonzero vector
       ` X ` . $)
    map1dimOLDlem1 $p |- ( ph -> G e. F ) $=
      ( clsm cfv csn clsh clspn c0g eqid chlt wcel wa clvec dvhlvec cdif eldifi
      syl snssd dochocsp clsa wne lveclmod eldifsn simprbi lsatlspsn dochsatshp
      clmod syl3anc eqeltrrd dochexmid lshpkrlem10 ) ACBDEHULUMZGQUNZNUMZIJKHUO
      UMZFHUPUMZEUQUMZOHQQUBUDWEURZWAURZWDURZAMUSUTPLUTVAHVBUTZUJHLMPSUAVCVFZAW
      BWEUMZNUMWCWDAHLMWENOPWBSUATUBWGUJAQOAQORUNZVDUTZQOUTZUKQOWMVEVFZVGVHAHVI
      UMZWLHLMNPWDSUATWQURZWIUJAHVPUTZWOQRVJZWLWQUTAWJWSWKHVKVFWPAWNWTUKWNWOWTQ
      ORVLVMVFWQWEOHQRUBWGUCWRVNVQVOVRWPWPAWAHLMWENOPQRSTUAUBUCWGWHUJUKVSUGUHUE
      WFURUIUFVT $.
      $( [27-Oct-1014] $)
  $}

  ${
    $d k v w A $.  $d k w D $.  $d k v w O $.  $d k v w R $.  $d k v w T $.
    $d k w U $.  $d k v w V $.  $d k v w X $.  $d k v w ph $.
    map1dimOLDlem4.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDlem4.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDlem4.m $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDlem4.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDlem4.v $e |- V = ( Base ` U ) $.
    map1dimOLDlem4.z $e |- Z = ( 0g ` U ) $.
    map1dimOLDlem4.a $e |- A = ( +g ` U ) $.
    map1dimOLDlem4.t $e |- T = ( vsca ` U ) $.
    map1dimOLDlem4.l $e |- L = ( LKer ` U ) $.
    map1dimOLDlem4.d $e |- D = ( Scalar ` U ) $.
    map1dimOLDlem4.r $e |- R = ( Base ` D ) $.
    map1dimOLDlem4.g $e |- G = ( v e. V |->
        ( iota_ k e. R E. w e. ( O ` { X } ) v = ( w A ( k T X ) ) ) ) $.
    map1dimOLDlem4.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDlem4.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    $( Kernel of a "canonical" functional ` G ` determined by a vector
       ` X ` . $)
    map1dimOLDlem4 $p |- ( ph -> ( L ` G ) = ( O ` { X } ) ) $=
      ( clsm cfv csn clfn clsh clspn c0g eqid chlt wcel wa clvec dvhlvec eldifi
      syl cdif snssd dochocsp clsa clmod wne lveclmod eldifsn simprbi lsatlspsn
      syl3anc dochsatshp eqeltrrd dochexmid lshpkrlem11 ) ACBDEHUNUOZGRUPZOUOZI
      HUQUOZJHURUOZFMHUSUOZEUTUOZPHRRUDUFWIVAZWDVAZWHVAZALVBVCQKVCVDHVEVCZULHKL
      QTUCVFVHZAWEWIUOZOUOWFWHAHKLWIOPQWETUCUAUDWKULARPARPSUPZVIVCZRPVCZUMRPWQV
      GVHZVJVKAHVLUOZWPHKLOQWHTUCUAXAVAZWMULAHVMVCZWSRSVNZWPXAVCAWNXCWOHVOVHWTA
      WRXDUMWRWSXDRPSVPVQVHXAWIPHRSUDWKUEXBVRVSVTWAWTWTAWDHKLWIOPQRSTUAUCUDUEWK
      WLULUMWBUIUJUGWJVAUKWGVAUHWC $.
      $( [27-Oct-1014] $)
  $}

  ${
    $d k v w A $.  $d k w E $.  $d k v w O $.  $d k v w R $.  $d k v w T $.
    $d k w U $.  $d k v w V $.  $d k v w X $.  $d k v w ph $.
    map1dimOLDlem5.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDlem5.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDlem5.i $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDlem5.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDlem5.v $e |- V = ( Base ` U ) $.
    map1dimOLDlem5.a $e |- A = ( +g ` U ) $.
    map1dimOLDlem5.t $e |- T = ( vsca ` U ) $.
    map1dimOLDlem5.z $e |- Z = ( 0g ` U ) $.
    map1dimOLDlem5.n $e |- N = ( LSpan ` U ) $.
    map1dimOLDlem5.e $e |- E = ( Scalar ` U ) $.
    map1dimOLDlem5.r $e |- R = ( Base ` E ) $.
    map1dimOLDlem5.d $e |- D = ( LDual ` U ) $.
    map1dimOLDlem5.s $e |- S = ( LSpan ` D ) $.
    map1dimOLDlem5.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDlem5.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    map1dimOLDlem5.g $e |- G = ( v e. V |->
        ( iota_ k e. R E. w e. ( O ` { X } ) v = ( w A ( k T X ) ) ) ) $.
    $( The value of the vector space mapping at a ray (span) of a vector ` X `
       is the ray of "canonical" functional ` G ` determined by ` X ` . $)
    map1dimOLDlem5 $p |- ( ph -> ( M ` ( N ` { X } ) ) = ( S ` { G } ) ) $=
      ( clsa cfv csn clfn clk eqid clmod wcel wne chlt dvhlmod syl cdif eldifsn
      wa eldifi simpr sylbi lsatlspsn syl3anc map1dimOLDlem1 map1dimOLDlem4 wss
      snssi dochocsp eqcomd eqtrd map1dimOLDval2 ) AIURUSZETUTZPUSZIIVAUSZLMNIV
      BUSZOGQSUBUCUDUEWFVCZWIVCZWJVCZUMUNUOAIVDVEZTRVEZTUAVFZWHWFVEANVGVESMVEVL
      WNUOIMNSUBUEVHVIATRUAUTZVJVEZWOUPTRWQVMVIZAWRWPUPWRWOWPVLWPTRUAVKWOWPVNVO
      VIWFPRITUAUFUJUIWKVPVQABCDKFHIJWILMNQRSTUAUBUCUEUFUIUGUHWLUKULUQUOUPVRALW
      JUSWGQUSZWHQUSZABCDKFHIJLMNWJOQRSTUAUBUCUDUEUFUIUGUHWMUKULUQUOUPVSAXAWTAI
      MNPQRSWGUBUEUCUFUJUOAWOWGRVTWSTRWAVIWBWCWDWE $.
      $( [27-Oct-1014] $)
  $}

  ${
    $d k v w A $.  $d k w D $.  $d k v w O $.  $d k v w R $.  $d k v w T $.
    $d k w U $.  $d k v w V $.  $d k v w X $.  $d k v w ph $.
    map1dimOLDlem6.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDlem6.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDlem6.i $e |- M = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDlem6.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDlem6.v $e |- V = ( Base ` U ) $.
    map1dimOLDlem6.a $e |- A = ( +g ` U ) $.
    map1dimOLDlem6.t $e |- T = ( vsca ` U ) $.
    map1dimOLDlem6.z $e |- Z = ( 0g ` U ) $.
    map1dimOLDlem6.n $e |- N = ( LSpan ` U ) $.
    map1dimOLDlem6.d $e |- D = ( Scalar ` U ) $.
    map1dimOLDlem6.r $e |- R = ( Base ` D ) $.
    map1dimOLDlem6.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDlem6.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    map1dimOLDlem6.g $e |- G = ( v e. V |->
        ( iota_ k e. R E. w e. ( O ` { X } ) v = ( w A ( k T X ) ) ) ) $.
    $( The "canonical" functional ` G ` determined by ` X ` belongs to the map
       value at the singleton of ` X ` . $)
    map1dimOLDlem6 $p |- ( ph -> G e. ( M ` ( N ` { X } ) ) ) $=
      ( csn cld cfv clspn clmod wcel eqid chlt dvhlmod lduallmod map1dimOLDlem1
      cbs wa syl clfn ldualvbase eleqtrrd lspsnid syl2anc map1dimOLDlem5 ) AJJU
      NHUOUPZUQUPZUPZRUNNUPMUPAVNURUSJVNVEUPZUSJVPUSAVNHVNUTZALVAUSQKUSVFHURUSU
      KHKLQTUCVBVGZVCAJHVHUPZVQABCDEFGHIVTJKLOPQRSTUAUCUDUGUEUFVTUTZUIUJUMUKULV
      DAVNVTVQHURWAVRVQUTZVSVIVJVOVQVNJWBVOUTZVKVLABCDVNFVOGHIEJKLMNOPQRSTUAUBU
      CUDUEUFUGUHUIUJVRWCUKULUMVMVJ $.
      $( [27-Oct-1014] $)
  $}

  ${
    $d a k v w A $.  $d k D $.  $d a k w I $.  $d a k v w O $.  $d a k v w R $.
    $d a k v w T $.  $d k w U $.  $d k v V $.  $d a k v w X $.  $d w Z $.
    $d k w ph $.
    map1dimOLDlem7.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDlem7.o $e |- O = ( ( ocH ` K ) ` W ) $.
    map1dimOLDlem7.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDlem7.v $e |- V = ( Base ` U ) $.
    map1dimOLDlem7.a $e |- A = ( +g ` U ) $.
    map1dimOLDlem7.t $e |- T = ( vsca ` U ) $.
    map1dimOLDlem7.z $e |- Z = ( 0g ` U ) $.
    map1dimOLDlem7.d $e |- D = ( Scalar ` U ) $.
    map1dimOLDlem7.r $e |- R = ( Base ` D ) $.
    map1dimOLDlem7.i $e |- I = ( 1r ` D ) $.
    map1dimOLDlem7.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDlem7.x $e |- ( ph -> X e. ( V \ { Z } ) ) $.
    map1dimOLDlem7.g $e |- G = ( v e. V |->
        ( iota_ k e. R E. w e. ( O ` { X } ) v = ( w A ( k T X ) ) ) ) $.
    $( The value of the "canonical" functional ` G ` equals 1 at the ` X ` that
       determines it. $)
    map1dimOLDlem7 $p |- ( ph -> ( G ` X ) = I ) $=
      ( va cfv cv co wceq csn wrex crio wcel eldifi syl eqeq1 rexbidv riotabidv
      cdif riotaex fvmpt clmod clss chlt dvhlmod wss snssd eqid dochlss syl2anc
      wa lss0cl lmodvs1 oveq2d 0vlid eqtr2d oveq1 eqeq2d rcla4ev wb crg lmodrng
      wreu rngidcl clsm clsh clspn clvec dvhlvec dochocsp clsa wne sylib simprd
      eldifsn lsatlspsn syl3anc dochsatshp eqeltrrd lshpkrlem3 ax-17 a17d mpbid
      dochexmid riota2f eqtrd ) AQJUMZQBUNZIUNZQGUOZDUOZUPZBQUQZNUMZURZIFUSZLAQ
      OUTZXNYCUPAQORUQZVFUTZYDUJQOYEVAVBZCQCUNZXRUPZBYAURZIFUSYCOJYHQUPZYJYBIFY
      KYIXSBYAYHQXRVCVDVEUKYBIFVGVHVBAQXOLQGUOZDUOZUPZBYAURZYCLUPZARYAUTZQRYLDU
      OZUPZYOAHVIUTZYAHVJUMZUTZYQAMVKUTPKUTVRZYTUIHKMPSUAVLVBZAUUCXTOVMUUBUIAQO
      YGVNZUUAHKMNOPXTSUAUBUUAVOZTVPVQUUAYAHRUEUUFVSVQAYRRQDUOZQAYLQRDAYTYDYLQU
      PUUDYGGLEOHQUBUFUDUHVTVQWAAYTYDUUGQUPUUDYGDOHQRUBUCUEWBVQWCYNYSBRYAXORUPY
      MYRQXORYLDWDWEWFVQALFUTZYBIFWJYOYPWGAEWHUTZUUHAYTUUIUUDEHUFWIVBFELUGUHWKV
      BABDEHWLUMZGYAIHWMUMZFHWNUMZOHQQUBUCUULVOZUUJVOZUUKVOZAUUCHWOUTUIHKMPSUAW
      PVBAXTUULUMZNUMYAUUKAHKMUULNOPXTSUATUBUUMUIUUEWQAHWRUMZUUPHKMNPUUKSUATUUQ
      VOZUUOUIAYTYDQRWSZUUPUUQUTUUDYGAYDUUSAYFYDUUSVRUJQORXBWTXAUUQUULOHQRUBUUM
      UEUURXCXDXEXFYGYGAUUJHKMUULNOPQRSTUAUBUEUUMUUNUIUJXKUFUGUDXGYBYOIULFLULUN
      LUTIXHUUHYOIXIXPLUPZXSYNBYAUUTXRYMQUUTXQYLXODXPLQGWDWAWEVDXLVQXJXM $.
      $( [27-Oct-1014] $)
  $}

  ${
    map1dimOLDth.h $e |- H = ( LHyp ` K ) $.
    map1dimOLDth.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    map1dimOLDth.v $e |- V = ( Base ` U ) $.
    map1dimOLDth.m $e |- M = ( -g ` U ) $.
    map1dimOLDth.r $e |- R = ( LSpan ` U ) $.
    map1dimOLDth.f $e |- F = ( LFnl ` U ) $.
    map1dimOLDth.d $e |- D = ( LDual ` U ) $.
    map1dimOLDth.s $e |- S = ( LSpan ` D ) $.
    map1dimOLDth.i $e |- I = ( ( map1dimOLD ` K ) ` W ) $.
    map1dimOLDth.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    map1dimOLDth.x $e |- ( ph -> X e. V ) $.
    map1dimOLDth.y $e |- ( ph -> Y e. V ) $.
    map1dimOLDth.g $e |- ( ph -> G e. F ) $.
    map1dimOLDth.n $e |- ( ph -> ( R ` { X } ) =/= ( R ` { Y } ) ) $.
    map1dimOLDth.e $e |- ( ph -> ( I ` ( R ` { X } ) ) = ( S ` { G } ) ) $.
    $( Part (1) of proof of the first fundamental theorem of projective
       geometry in [Baer] p. 44. $)
$(
    map1dimOLDth $p |- ( ph ->
              E! h e. F ( ( I ` ( R ` { Y } ) ) = ( S ` { h } )
           /\ ( I ` ( R ` { ( X M Y ) } ) ) = ( S ` { ( G oF M h ) } ) ) ) $=
      (  ) ? $.
$)
      $( [24-Oct-1014] $)
  $}

  ${
    $d p A $.  $d p S $.  $d p T $.  $d p U $.  $d p W $.
    lssatle.s $e |- S = ( LSubSp ` W ) $.
    lssatle.a $e |- A = ( LSAtoms ` W ) $.
    lssatle.w $e |- ( ph -> W e. LMod ) $.
    lssatle.t $e |- ( ph -> T e. S ) $.
    lssatle.u $e |- ( ph -> U e. S ) $.
    $( The ordering of two subspaces is determined by the atoms under them.
       ( ~ chrelat3 analog.) $)
    lssatle $p |- ( ph
       -> ( T C_ U <-> A. p e. A ( p C_ T -> p C_ U ) ) ) $=
      ( wss crab cuni cfv clmod wcel wceq syl2anc cv wi wral sstrALT2 ralrimivw
      expcom ss2rab clspn cbs adantr lsatlss syl rabss2 uniss 3syl unimax lssss
      wa eqid eqsstrd sstrd adantl lspss syl3anc lssats sseq12d sylibrd syl5bir
      ex impbid2 ) ADEMZGUAZDMZVLEMZUBZGBUCZVKVOGBVMVKVNVLDEUDUFUEVPVMGBNZVNGBN
      ZMZAVKVMVNGBUGAVSVQOZFUHPZPZVROZWAPZMZVKAVSWEAVSURFQRZWCFUIPZMZVTWCMZWEAW
      FVSJUJAWHVSAWCVNGCNZOZWGABCMZVRWJMWCWKMAWFWLJBCFHIUKULVNGBCUMVRWJUNUOAWKE
      WGAECRZWKESLGECUPULAWFWMEWGMJLCEWGFQWGUSZHUQTUTVAUJVSWIAVQVRUNVBVTWCWAWGF
      WNWAUSZVCVDVIADWBEWDAWFDCRDWBSJKGBCDWAFHWOIVETAWFWMEWDSJLGBCEWAFHWOIVETVF
      VGVHVJ $.
      $( [29-Oct-1014] $)
  $}

  ${
    $d v P $.  $d v Q $.  $d v T $.  $d v U $.  $d v W $.  $d v ph $.
    lsmsatcv.s $e |- S = ( LSubSp ` W ) $.
    lsmsatcv.p $e |- P = ( LSSum ` W ) $.
    lsmsatcv.a $e |- A = ( LSAtoms ` W ) $.
    lsmsatcv.w $e |- ( ph -> W e. LVec ) $.
    lsmsatcv.t $e |- ( ph -> T e. S ) $.
    lsmsatcv.u $e |- ( ph -> U e. S ) $.
    lsmsatcv.x $e |- ( ph -> Q e. A ) $.
    $( Subspace sum has the covering property (using spans of singletons to
       represent atoms).  Similar to Exercise 5 of [Kalmbach] p. 153.
       ( ~ spansncvi analog.)  Explicit atom version of ~ lsmcv . $)
    lsmsatcv $p |- ( ( ph /\ T C. U /\ U C_ ( T P Q ) )
      -> U = ( T P Q ) ) $=
      ( vv wceq cfv wa wcel wpss co wss cv csn clspn wrex wi clvec eqid islsati
      cbs syl2anc w3a eqid1 adantr simpr 3expib 3adant3 wb sseq2d anbi2d eqeq2d
      lsmcv oveq2 imbi12d 3ad2ant3 mpbird 3exp rexlimdv mpd 3impib ) AFGUAZGFDC
      UBZUCZGVNQZADPUDZUEHUFRZRZQZPHULRZUGZVMVOSZVPUHZAHUITZDBTWBLOPBDVRWAHUIWA
      UJVRUJKUKUMAVTWDPWAAVQWATZVTWDAWFVTUNWDVMGFVSCUBZUCZSZGWGQZUHZAWFWKVTAWFS
      ZVMWHWJWLCEFGVRWAHVQWAUOIVRUOJAWEWFLUPAFETWFMUPAGETWFNUPAWFUQVDURUSVTAWDW
      KUTWFVTWCWIVPWJVTVOWHVMVTVNWGGDVSFCVEZVAVBVTVNWGGWMVCVFVGVHVIVJVKVL $.
      $( [29-Oct-1014] $)
  $}

  ${
    lshpin.h $e |- H = ( LSHyp ` W ) $.
    lshpin.w $e |- ( ph -> W e. LVec ) $.
    lshpin.t $e |- ( ph -> T e. H ) $.
    lshpin.u $e |- ( ph -> U e. H ) $.
    $( The intersection of two different hyperplanes is not a hyperplane. $)
    lshpin $p |- ( ph -> ( ( T i^i U ) e. H <-> T = U ) ) $=
      ( cin wcel wceq wa wss inss1 a1i clvec adantr lshpcmp mpbid eqcomd eleq1d
      simpr inss2 eqtrd ex inidm syl5eqel ineq2 syl5ibcom impbid ) ABCJZDKZBCLZ
      AUMUNAUMMZBULCUOULBUOULBNZULBLUPUOBCOPUOULBDEFAEQKUMGRZAUMUCZABDKUMHRSTUA
      UOULCNZULCLUSUOBCUDPUOULCDEFUQURACDKUMIRSTUEUFABBJZDKUNUMAUTBDBUGHUHUNUTU
      LDBCBUIUBUJUK $.
      $( [29-Oct-1014] $)
  $}

  ${
    dochsat.h $e |- H = ( LHyp ` K ) $.
    dochsat.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochsat.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochsat.s $e |- S = ( LSubSp ` U ) $.
    dochsat.a $e |- A = ( LSAtoms ` U ) $.
    dochsat.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochsat.q $e |- ( ph -> Q e. S ) $.
    $( The double orthocomplement of an atom is an atom. $)
    dochsat $p |- ( ph -> ( ( O ` ( O ` Q ) ) e. A <-> Q e. A ) ) $=
      ( cfv wcel wceq adantr wa c0g csn clsm co wpss wss wne clmod chlt dvhlvec
      clvec syl lveclmod eqid lss0ss syl2anc simpr lsatn0 cbs doch0 doch1 eqtrd
      fveq2d ex necon3d necomd sylanbrc lssss dochocss lsatlssel lsm02 sseqtr4d
      mpd df-pss lsssn0 lsmsatcv mpd3an23 eqtr2d eqeltrrd cdih dih1dimat dochoc
      crn sylan eqeltrd impbida ) ACHQZHQZBRZCBRZAWJUAZWICBWLCEUBQZUCZWIEUDQZUE
      ZWIWLWNCUFZCWPUGCWPSWLWNCUGZWNCUHWQWLEUIRZCDRZWRAWSWJAEULRZWSAGUJRIFRUAZX
      AOEFGIJLUKUMZEUNUMTZAWTWJPTZDECWMWMUOZMUPUQWLCWNWLWIWNUHCWNUHWLBWIWMEXFNX
      DAWJURZUSWLCWNWIWNWLCWNSZWIWNSWLXHUAZWIWNHQZHQZWNXIWHXJHXICWNHWLXHURVDVDW
      LXKWNSZXHAXLWJAXKEUTQZHQZWNAXJXMHAXBXJXMSOEFGHXMIWMJLKXMUOZXFVAUMVDAXBXNW
      NSOEFGHXMIWMJLKXOXFVBUMVCTTVCVEVFVNVGWNCVOVHWLCWIWPWLXBCXMUGZCWIUGAXBWJOT
      AXPWJAXAWTXPXCPDCXMEULXOMVIUQTEFGHXMICJLXOKVJUQWLWSWIDRWPWISXDWLBDWIEMNXD
      XGVKWODEWIWMXFMWOUOZVLUQZVMWLBWOWIDWNCEMXQNAXAWJXCTWLWSWNDRXDDEWMXFMVPUMX
      EXGVQVRXRVSXGVTAWKUAZWICBXSXBCIGWAQQZWDRZWICSAXBWKOTAXBWKYAOBCEFXTGIJLXTU
      OZNWBWEFXTGHICJYBKWCUQAWKURWFWG $.
      $( [29-Oct-1014] $)
  $}

  ${
    $d v O $.  $d v U $.  $d v V $.  $d v X $.  $d v ph $.
    dochshpncl.h $e |- H = ( LHyp ` K ) $.
    dochshpncl.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochshpncl.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochshpncl.v $e |- V = ( Base ` U ) $.
    dochshpncl.y $e |- Y = ( LSHyp ` U ) $.
    dochshpncl.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochshpncl.x $e |- ( ph -> X e. Y ) $.
    $( If a hyperplane is not closed, its closure equals the vector space. $)
    dochshpncl $p |- ( ph
         -> ( ( O ` ( O ` X ) ) =/= X <-> ( O ` ( O ` X ) ) = V ) ) $=
      ( cfv wcel adantr wss vv wne wceq wa cv csn clspn clsm wrex clss w3a eqid
      chlt clmod dvhlmod syl lshpsmel mpbid simp3d wpss adantlr 3adant3 lshplss
      co id lssss syl2anc dochocss 3ad2ant1 simp1r necomd df-pss sylanbrc simp3
      dochssv sseqtr4d clvec dvhlvec dochlss simpr lsmcv syl3anc eqtrd rexlimdv
      3exp mpd lshpne eqnetrd impbida ) AHEQZEQZHUBZWKFUCZAWLUDZHUAUEZUFBUGQZQB
      UHQZVDZFUCZUAFUIZWMAWTWLAHBUJQZRZHFUBZWTAHIRXBXCWTUKPAUAWQXAHIWPFBMWPULZX
      AULZWQULZNADUMRGCRUDZBUNRZOBCDGJLUOUPZUQURUSSWNWSWMUAFWNWOFRZWSWMWNXJWSUK
      ZWKWRFXKAXJUDZHWKUTZWKWRTWKWRUCWNXJXLWSAXJXLWLXLVEVAVBXKHWKTZHWKUBXMWNXJX
      NWSAXNWLAXGHFTZXNOAXHXBXOXIAXAHIBXENXIPVCZXAHFBUNMXEVFVGZBCDEFGHJLMKVHVGS
      VIXKWKHAWLXJWSVJVKHWKVLVMXKWKFWRWNXJWKFTZWSAXRWLAXGWJFTZXROAXGXOXSOXQBCDE
      FGHJLMKVOVGZBCDEFGWJJLMKVOVGSVIWNXJWSVNZVPXLWQXAHWKWPFBWOMXEXDXFXLXGBVQRA
      XGXJOSBCDGJLVRUPAXBXJXPSAWKXARZXJAXGXSYBOXTXABCDEFGWJJLMXEKVSVGSAXJVTWAWB
      YAWCWEWDWFAWMUDZWKFHAWMVTYCHFAXCWMAHIFBMNXIPWGSVKWHWI $.
      $( [29-Oct-1014] $)
  $}

  ${
    $d v A $.  $d v O $.  $d v Q $.  $d v U $.  $d v Y $.  $d v ph $.
    dochsatshpb.h $e |- H = ( LHyp ` K ) $.
    dochsatshpb.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochsatshpb.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochsatshpb.s $e |- S = ( LSubSp ` U ) $.
    dochsatshpb.a $e |- A = ( LSAtoms ` U ) $.
    dochsatshpb.y $e |- Y = ( LSHyp ` U ) $.
    dochsatshpb.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochsatshpb.q $e |- ( ph -> Q e. S ) $.
    $( The orthocomplement of a subspace atom is a hyperplane. $)
    dochsatshpb $p |- ( ph -> ( Q e. A <-> ( O ` Q ) e. Y ) ) $=
      ( wcel cfv vv wa chlt adantr simpr dochsatshp c0g wne wrex csn eqid clmod
      cv cbs dvhlmod syl lshpne wceq doch0 neeqtrrd cdih wss crn syl2anc dochcl
      lssss lsssn0 doch11 necon3bid mpbird dih0rn dochoc neeqtrd dochlss lssne0
      wb mpbid w3a clspn 3ad2ant1 simp2 lspsnss syl3anc lssel dihlsprn eqsstr3d
      dochord clvec dvhlvec simp1r simp3 lsatlspsn lshpcmp fveq2d eqtrd eqeltrd
      3exp rexlimdv mpd dochsat impbida ) ACBSZCHTZJSZAXBUBBCEFGHIJKMLOPAGUCSIF
      SUBZXBQUDAXBUEUFAXDUBZXCHTZBSZXBXFUAUMZEUGTZUHZUAXGUIZXHXFXGXJUJZUHZXLXFX
      GXMHTZHTZXMXFXGXPUHXCXOUHXFXCEUNTZXOXFXCJXQEXQUKZPAEULSZXDAXEXSQEFGIKMUOU
      PZUDZAXDUEUQAXOXQURZXDAXEYBQEFGHXQIXJKMLXRXJUKZUSUPUDUTXFXGXPXCXOAXGXPURX
      CXOURVPXDAFIGVATTZGHIXCXOKYDUKZLQAXECXQVBZXCYDVCZSZQAXSCDSZYFXTRDCXQEULXR
      NVFVDZEFYDGHXQICKYEMXRLVEVDZAXEXMXQVBZXOYGSQAXSXMDSZYLXTAXSYMXTDEXJYCNVGU
      PDXMXQEULXRNVFVDEFYDGHXQIXMKYEMXRLVEVDVHUDVIVJAXPXMURZXDAXEXMYGSZYNQAXEYO
      QEFYDGXJIKYEMYCVKUPFYDGHIXMKYELVLVDUDVMXFXSXGDSZXNXLVPYAAYPXDAXEXCXQVBZYP
      QAXSXCDSZYQXTAXEYFYRQYJDEFGHXQICKMXRNLVNVDDXCXQEULXRNVFVDZDEFGHXQIXCKMXRN
      LVNVDUDZUADEXGXJYCNVOVDVQXFXKXHUAXGXFXIXGSZXKXHXFUUAXKVRZXGXIUJEVSTZTZBUU
      BXGUUDHTZHTZUUDUUBXCUUEHUUBXCUUEVBXCUUEURUUBXCXGHTZUUEUUBXEYHUUGXCURXFUUA
      XEXKAXEXDQUDZVTZXFUUAYHXKAYHXDYKUDVTFYDGHIXCKYELVLVDUUBUUDXGVBZUUGUUEVBUU
      BXSYPUUAUUJXFUUAXSXKYAVTZXFUUAYPXKYTVTZXFUUAXKWAZDXGUUCEXINUUCUKZWBWCUUBF
      YDGHIUUDXGKYELUUIUUBXEXIXQSZUUDYGSZUUIUUBXSYPUUAUUOUUKUULUUMDXGXQEXIULXRN
      WDWCZEFYDGUUCXQIXIKMXRUUNYEWEVDZXFUUAXGYGSZXKAUUSXDAXEYQUUSQYSEFYDGHXQIXC
      KYEMXRLVEVDUDVTWGVQWFUUBXCUUEJEPXFUUAEWHSZXKAUUTXDAXEUUTQEFGIKMWIUPUDVTAX
      DUUAXKWJUUBBUUDEFGHIJKMLOPUUIUUBXSUUOXKUUDBSUUKUUQXFUUAXKWKBUUCXQEXIXJXRU
      UNYCOWLWCZUFWMVQWNUUBXEUUPUUFUUDURUUIUURFYDGHIUUDKYELVLVDWOUVAWPWQWRWSXFB
      CDEFGHIKLMNOUUHAYIXDRUDWTVQXA $.
      $( [29-Oct-1014] $)
  $}

  ${
    dochshpsat.h $e |- H = ( LHyp ` K ) $.
    dochshpsat.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochshpsat.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochshpsat.a $e |- A = ( LSAtoms ` U ) $.
    dochshpsat.y $e |- Y = ( LSHyp ` U ) $.
    dochshpsat.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochshpsat.x $e |- ( ph -> X e. Y ) $.
    $( A hyperplane is closed iff its orthocomplement is an atom. $)
    dochshpsat $p |- ( ph
         -> ( ( O ` ( O ` X ) ) = X <-> ( O ` X ) e. A ) ) $=
      ( cfv wceq wcel adantr wa simpr eqeltrd wb clss eqid chlt cbs wss dvhlmod
      clmod syl eqid1 lshplss syl2anc dochlss dochsatshpb mpbird wn c0g csn wne
      lssss lsatn0 df-ne sylib doch0 eqeq2d crn dochcl dih0rn doch11 dochshpncl
      cdih bitr3d mtbird necon1bbid mpbid impbida ) AHFQZFQZHRZVTBSZAWBUAZWCWAI
      SZWDWAHIAWBUBAHISWBPTUCAWCWEUDWBABVTCUEQZCDEFGIJKLWFUFZMNOAEUGSGDSUAZHCUH
      QZUIZVTWFSOACUKSZHWFSWJAWHWKOCDEGJLUJULZAWFHICWFUMNWLPUNWFHWICUKWIUFZWGVC
      UOZWFCDEFWIGHJLWMWGKUPUOUQTURAWCUAZWAWIRZUSZWBWOWPVTCUTQZVAZRZWOVTWSVBWTU
      SWOBVTWRCWRUFZMAWKWCWLTAWCUBVDVTWSVEVFWOWAWSFQZRZWPWTWOXBWIWAWOWHXBWIRAWH
      WCOTCDEFWIGWRJLKWMXAVGULVHAXCWTUDWCADGEVNQQZEFGVTWSJXDUFZKOAWHWJVTXDVIZSO
      WNCDXDEFWIGHJXDUMLWMKVJUOAWHWSXFSOCDXDEWRGJXELXAVKULVLTVOVPAWQWBUDWCAWPWA
      HACDEFWIGHIJKLWMNOPVMVQTVRVS $.
      $( [29-Oct-1014] $)
  $}

  ${
    dochlkr.h $e |- H = ( LHyp ` K ) $.
    dochlkr.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochlkr.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochlkr.f $e |- F = ( LFnl ` U ) $.
    dochlkr.y $e |- Y = ( LSHyp ` U ) $.
    dochlkr.l $e |- L = ( LKer ` U ) $.
    dochlkr.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochlkr.g $e |- ( ph -> G e. F ) $.
    $( Equivalent conditions for the closure of a kernel to be a hyperplane. $)
    dochlkr $p |- ( ph
     -> ( ( O ` ( O ` ( L ` G ) ) ) e. Y
       <-> ( ( O ` ( O ` ( L ` G ) ) ) = ( L ` G )
                /\ ( L ` G ) e. Y ) ) ) $=
      ( cfv wcel wceq wa wss chlt cbs clmod clss dvhlmod syl eqid syl2anc lssss
      lkrlss dochocss adantr clvec dvhlvec wne simpr lshpne lkrshpor ord fveq2d
      ex wn fveq2 adantl cdih crn dih1rn dochoc eqtrd syld necon1ad imp lshpcmp
      mpbid eqcomd jca eleq1 biimpar impbid1 ) ADGSZHSZHSZJTZWEWCUAZWCJTZUBZAWF
      WIAWFUBZWGWHWJWCWEWJWCWEUCZWCWEUAAWKWFAFUDTIETUBZWCBUESZUCZWKQABUFTZWCBUG
      SZTZWNAWLWOQBEFIKMUHUIZAWODCTWQWRRWPCDGBNPWPUJZUMUKWPWCWMBUFWMUJZWSULUKBE
      FHWMIWCKMWTLUNUKUOWJWCWEJBOABUPTZWFAWLXAQBEFIKMUQUIZUOAWFWHAWFWEWMURZWHAW
      FXCWJWEJWMBWTOAWOWFWRUOAWFUSZUTVDAWHWEWMAWHVEWCWMUAZWEWMUAZAWHXEACDJGWMBW
      TONPXBRVAVBAXEXFAXEUBZWEWMHSZHSZWMXEWEXIUAAXEWDXHHWCWMHVFVCVGXGWLWMIFVHSS
      ZVITZXIWMUAAWLXEQUOZXGWLXKXLBEXJFWMIKXJUJZMWTVJUIEXJFHIWMKXMLVKUKVLVDVMVN
      VMVOZXDVPVQVRXNVSVDWGWFWHWEWCJVTWAWB $.
      $( [29-Oct-1014] $)
  $}

  ${
    lkrshp4.v $e |- V = ( Base ` W ) $.
    lkrshp4.h $e |- H = ( LSHyp ` W ) $.
    lkrshp4.f $e |- F = ( LFnl ` W ) $.
    lkrshp4.k $e |- K = ( LKer ` W ) $.
    lkrshp4.w $e |- ( ph -> W e. LVec ) $.
    lkrshp4.g $e |- ( ph -> G e. F ) $.
    $( A kernel is a hyperplane iff it doesn't contain all vectors. $)
    lkrshp4 $p |- ( ph -> ( ( K ` G ) =/= V <-> ( K ` G ) e. H ) ) $=
      ( cfv wne wcel wceq wo wi lkrshpor orcomd neor sylib clmod clvec lveclmod
      wa syl adantr simpr lshpne ex impbid ) ACENZFOZUNDPZAUNFQZUPRUOUPSAUPUQAB
      CDEFGHIJKLMTUAUPUNFUBUCAUPUOAUPUGUNDFGHIAGUDPZUPAGUEPURLGUFUHUIAUPUJUKULU
      M $.
      $( [1-Nov-1014] $)
  $}

  ${
    dochkrshp.h $e |- H = ( LHyp ` K ) $.
    dochkrshp.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochkrshp.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochkrshp.v $e |- V = ( Base ` U ) $.
    dochkrshp.y $e |- Y = ( LSHyp ` U ) $.
    dochkrshp.f $e |- F = ( LFnl ` U ) $.
    dochkrshp.l $e |- L = ( LKer ` U ) $.
    dochkrshp.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochkrshp.g $e |- ( ph -> G e. F ) $.
    $( The closure of a kernel is a hyperplane iff it doesn't contain all
       vectors. $)
    dochkrshp $p |- ( ph -> ( ( O ` ( O ` ( L ` G ) ) ) =/= V
              <-> ( O ` ( O ` ( L ` G ) ) ) e. Y ) ) $=
      ( cfv wne wcel wceq wa simpr chlt adantr fveq2 fveq2d cdih crn dih1rn syl
      eqid1 eqid dochoc syl2anc sylan9eqr eqtr4d ex necon3d df-ne clvec dvhlvec
      wn lkrshpor orcomd ord syl5bi syld dochshpncl mpbid necon1d necon3ad jcad
      imp dochlkr sylibrd clmod dvhlmod lshpne impbid ) ADGUAZHUAZHUAZIUBZWFKUC
      ZAWGWFWDUDZWDKUCZUEWHAWGWIWJAWFWDWFIAWFWDUBZWFIUDZAWKUEZWKWLAWKUFWMBEFHIJ
      WDKLMNOPAFUGUCJEUCUEZWKSUHAWKWJAWKWDIUBZWJAWDIWFWDAWDIUDZWIAWPUEWFIWDWPAW
      FIHUAZHUAZIWPWEWQHWDIHUIUJAWNIJFUKUAUAZULUCZWRIUDSAWNWTSBEWSFIJLWSUONOUMU
      NEWSFHJILWSUPMUQURUSZAWPUFUTVAVBWOWPVFZAWJWDIVCAWPWJAWJWPACDKGIBOPQRAWNBV
      DUCSBEFJLNVEUNTVGVHVIZVJVKVQVLVMVAVNAWGXBWJAWPWFIAWPWLXAVAVOXCVKVPABCDEFG
      HJKLMNQPRSTVRVSAWHWGAWHUEWFKIBOPABVTUCZWHAWNXDSBEFJLNWAUNUHAWHUFWBVAWC $.
      $( [1-Nov-1014] $)
  $}

  ${
    dochkrsat.h $e |- H = ( LHyp ` K ) $.
    dochkrsat.o $e |- O = ( ( ocH ` K ) ` W ) $.
    dochkrsat.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    dochkrsat.a $e |- A = ( LSAtoms ` U ) $.
    dochkrsat.f $e |- F = ( LFnl ` U ) $.
    dochkrsat.l $e |- L = ( LKer ` U ) $.
    dochkrsat.z $e |- Z = ( 0g ` U ) $.
    dochkrsat.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    dochkrsat.g $e |- ( ph -> G e. F ) $.
    $( The orthocomplement of a kernel is an atom iff it is nonzero. $)
    dochkrsat $p |- ( ph
         -> ( ( O ` ( L ` G ) ) =/= { Z } <-> ( O ` ( L ` G ) ) e. A ) ) $=
      ( cfv cbs wne clsh wcel eqid dochkrshp wceq fveq2 chlt wa doch0 sylan9eqr
      csn syl cdih crn clmod clss dvhlmod lkrlss syl2anc lssss dochcl sylan9req
      dochoc doch1 adantr eqtrd impbida necon3bid dochlss dochsatshpb 3bitr4d
      wss ) AEHUAZIUAZIUAZCUBUAZUCVRCUDUAZUEVQKUNZUCVQBUEACDEFGHIVSJVTLMNVSUFZV
      TUFZPQSTUGAVQWAVRVSAVQWAUHZVRVSUHZWDAVRWAIUAZVSVQWAIUIAGUJUEJFUEUKZWFVSUH
      SCFGIVSJKLNMWBRULUOUMAWEUKVQVSIUAZWAAWEVQVRIUAZWHAWGVQJGUPUAUAZUQUEZWIVQU
      HSAWGVPVSVOZWKSACURUEZVPCUSUAZUEZWLAWGWMSCFGJLNUTUOZAWMEDUEWOWPTWNDEHCPQW
      NUFZVAVBWNVPVSCURWBWQVCVBZCFWJGIVSJVPLWJUFZNWBMVDVBFWJGIJVQLWSMVFVBVRVSIU
      IVEAWHWAUHZWEAWGWTSCFGIVSJKLNMWBRVGUOVHVIVJVKABVQWNCFGIJVTLMNWQOWCSAWGWLV
      QWNUESWRWNCFGIVSJVPLNWBWQMVLVBVMVN $.
      $( [1-Nov-1014] $)
  $}

  ${
    $d g F $.  $d g J $.  $d g L $.  $d g O $.  $d g Y $.
    mapdOLDordlem1.t $e |- T = { g e. F | ( O ` ( O ` ( L ` g ) ) ) e. Y } $.
    $( Lemma for ~ mapdOLDord . $)
    mapdOLDordlem1 $p |- ( J e. T
          <-> ( J e. F /\ ( O ` ( O ` ( L ` J ) ) ) e. Y ) ) $=
      ( cv cfv wcel wceq fveq2 fveq2d eleq1d elrab2 ) BIZEJZFJZFJZGKDEJZFJZFJZG
      KBDCAQDLZTUCGUDSUBFUDRUAFQDEMNNOHP $.
      $( [30-Oct-1014] $)
  $}

  ${
    $d f g K $.  $d f g p U $.  $d f g W $.
    mapdOLDord.h $e |- H = ( LHyp ` K ) $.
    mapdOLDord.i $e |- I = ( ( DIsoH ` K ) ` W ) $.
    mapdOLDord.u $e |- U = ( ( DVecH ` K ) ` W ) $.
    mapdOLDord.m $e |- M = ( ( mapdOLD ` K ) ` W ) $.
    mapdOLDord.k $e |- ( ph -> ( K e. HL /\ W e. H ) ) $.
    mapdOLDord.x $e |- ( ph -> X e. ran I ) $.
    mapdOLDord.y $e |- ( ph -> Y e. ran I ) $.
    ${
      $d f p A $.  $d g C $.  $d f g F $.  $d f g p L $.  $d f g p O $.
      $d p T $.  $d f p X $.  $d f p Y $.  $d f p ph $.
      mapdOLDord.o $e |- O = ( ( ocH ` K ) ` W ) $.
      mapdOLDord.a $e |- A = ( LSAtoms ` U ) $.
      mapdOLDord.f $e |- F = ( LFnl ` U ) $.
      mapdOLDord.c $e |- C = ( LSHyp ` U ) $.
      mapdOLDord.l $e |- L = ( LKer ` U ) $.
      mapdOLDord.t $e |- T = { g e. F | ( O ` ( O ` ( L ` g ) ) ) e. C } $.
      $( Lemma for ~ mapdOLDord .  Ordering property of the D map. $)
      mapdOLDordlem2 $p |- ( ph -> ( ( M ` X ) C_ ( M ` Y ) <-> X C_ Y ) ) $=
        ( vf vp cfv wss cv crab clss chlt eqid wcel dihrnlss syl2anc mapdOLDval
        wa crn eqid1 sseq12d wi wral ss2rab mapdOLDordlem1 simprl adantr simprr
        wceq dochlkr mpbid simpld eqcomd cbs clmod dvhlmod lkrlss lssss dochlss
        dochcl eqeltrd dochord2 imbi12d biimpd embantd ex syl5bi com23 ralimdv2
        syl lssatle simprbi adantl simplbi simprd dochshpsat wrex clvec dvhlvec
        wex simpr dochsatshp lshpkr df-rex sylib fveq2d lsatlssel dochoc sylibr
        eqtrd jca dih1dimat eximdv mpd wb sseq1 ralxfrd bitr2d ad2antrr dochord
        sylibd sstr a1i mpand ss2rabdv impbid bitrd ) AOLULZPLULZUMOMULZUJUNZKU
        LZUMZUJGUOZPMULZYQUMZUJGUOZUMZOPUMZAYMYSYNUUBAEUPULZOEUJGHJKLMUQNQSUUEU
        RZUFUHUDTUAAJUQUSNHUSVCZOIVDZUSZOUUEUSUAUBUUEEHIJNOQSRUUFUTVAZVBAUUEPEU
        JGHJKLMUQNQSUUEVEUFUHUDTUAAUUGPUUHUSZPUUEUSUAUCUUEEHIJNPQSRUUFUTVAZVBVF
        AUUCUUDAUUCYQMULZOUMZUUMPUMZVGZUJDVHZUUDUUCYRUUAVGZUJGVHAUUQYRUUAUJGVIA
        UURUUPUJGDAYPDUSZYPGUSZUURVGZUUPUUSUUTUUMMULZCUSZVCZAUVAUUPVGZDFGYPKMCU
        IVJZAUVDUVEAUVDVCZUUTUURUUPAUUTUVCVKZUVGUURUUPUVGYRUUNUUAUUOUVGHIJMNOYQ
        QRUDAUUGUVDUAVLZAUUIUVDUBVLUVGYQUVBUUHUVGUVBYQUVGUVBYQVNZYQCUSZUVGUVCUV
        JUVKVCZAUUTUVCVMUVGEGYPHJKMNCQUDSUFUGUHUVIUVHVOVPVQVRUVGUUGUUMEVSULZUMZ
        UVBUUHUSUVIUVGEVTUSZUUMUUEUSZUVNAUVOUVDAUUGUVOUAEHJNQSWAWOZVLZUVGUUGYQU
        VMUMZUVPUVIUVGUVOYQUUEUSZUVSUVRUVGUVOUUTUVTUVRUVHUUEGYPKEUFUHUUFWBVAUUE
        YQUVMEVTUVMURZUUFWCVAUUEEHJMUVMNYQQSUWAUUFUDWDVAUUEUUMUVMEVTUWAUUFWCVAE
        HIJMUVMNUUMQRSUWAUDWEVAWFZWGUVGHIJMNPYQQRUDUVIAUUKUVDUCVLUWBWGWHWIWJWKW
        LWMWNWLAUUDUKUNZOUMZUWCPUMZVGZUKBVHUUQABUUEOPEUKUUFUEUVQUUJUULWPAUWFUUP
        UKUJUUMBDAUUSVCZUVJUUMBUSUWGUVJUVKUWGUVCUVLUUSUVCAUUSUUTUVCUVFWQWRUWGEG
        YPHJKMNCQUDSUFUGUHAUUGUUSUAVLZUUSUUTAUUSUUTUVCUVFWSWRVOVPZVQUWGBEHJMNYQ
        CQUDSUEUGUWHUWGUVJUVKUWIWTXAVPAUWCBUSZVCZUUSUWCUUMVNZVCZUJXEZUWLUJDXBUW
        KUUTYQUWCMULZVNZVCZUJXEZUWNUWKUWPUJGXBZUWRUWKEXCUSZUWOCUSZUWSAUWTUWJAUU
        GUWTUAEHJNQSXDWOVLUWKBUWCEHJMNCQSUDUEUGAUUGUWJUAVLZAUWJXFZXGZUWOUJGCKEU
        GUFUHXHVAUWPUJGXIXJUWKUWQUWMUJUWKUWQUWMUWKUWQVCZUUSUWLUXEUVDUUSUXEUUTUV
        CUWKUUTUWPVKUXEUVBUWOCUXEUVBUWOMULZMULZUWOUXEUUMUXFMUXEYQUWOMUWKUUTUWPV
        MXKZXKUWKUXGUWOVNZUWQUWKUUGUWOUUHUSZUXIUXBUWKUUGUWCUVMUMZUXJUXBUWKUVOUW
        CUUEUSUXKAUVOUWJUVQVLZUWKBUUEUWCEUUFUEUXLUXCXLUUEUWCUVMEVTUWAUUFWCVAEHI
        JMUVMNUWCQRSUWAUDWEVAHIJMNUWOQRUDXMVAVLXOUWKUXAUWQUXDVLWFXPUVFXNUXEUUMU
        WCUXEUUMUXFUWCUXHUWKUXFUWCVNZUWQUWKUUGUWCUUHUSZUXMUXBUWKUUGUWJUXNUXBUXC
        BUWCEHIJNQSRUEXQVAHIJMNUWCQRUDXMVAVLXOVRXPWKXRXSUWLUJDXIXNUWLUWFUUPXTAU
        WLUWDUUNUWEUUOUWCUUMOYAUWCUUMPYAWHWRYBYCYFAUUDUUCAUUDVCZYRUUAUJGUXOUUTV
        CZYTYOUMZYRUUAUXPUUDUXQUXOUUDUUTAUUDXFVLUXPHIJMNOPQRUDAUUGUUDUUTUAYDAUU
        IUUDUUTUBYDAUUKUUDUUTUCYDYEVPUXQYRVCUUAVGUXPYTYOYQYGYHYIYJWKYKYL $.
        $( [29-Oct-1014] $)
    $}

    $( Ordering property of the D map. $)
    mapdOLDord $p |- ( ph -> ( ( M ` X ) C_ ( M ` Y ) <-> X C_ Y ) ) $=
      ( vg clsa cfv eqid clsh cv clk coch wcel clfn crab mapdOLDordlem2 ) ABRSZ
      BUASZQUBBUCSZSGEUDSSZSULSUJUEQBUFSZUGZBQUMCDEUKFULGHIJKLMNOPULTUITUMTUJTU
      KTUNTUH $.
      $( [30-Oct-1014] $)
  $}



$( (End of Norm Megill's mathbox.) $)
