$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of relationships
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.
    epelcNEW.1 $e |- B e. _V $.
    $( The epsilon relationship and the membership relation are the same.
       (Contributed by Scott Fenton, 11-Apr-2012.) $)
    epelcNEW $p |- ( A _E B <-> A e. B ) $=
      ( vx vy cep wbr cvv wcel rele brrelexi elex wb eleq1 eleq2 df-eprel brabg
      wel cv mpan2 pm5.21nii ) ABFGZAHIZABIZABFJKABLUCBHIUBUDMCDERAESZIUDDEABHH
      FDSAUENUEBAODEPQTUA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The epsilon relation and membership are the same.  General version of
       ~ epel .  (Contributed by Scott Fenton, 27-Mar-2011.) $)
    epelg $p |- ( B e. V -> ( A _E B <-> A e. B ) ) $=
      ( vx cv cep wbr wcel breq2 eleq2 vex epelcNEW vtoclbg ) ADEZFGANHABFGABHD
      BCNBAFINBAJANDKLM $.
      $( [27-Mar-2011] $)
  $}

  ${
    brtp.1 $e |- X e. _V $.
    brtp.2 $e |- Y e. _V $.
    brtp.3 $e |- B e. _V $.
    brtp.4 $e |- D e. _V $.
    brtp.5 $e |- F e. _V $.
    $( A condition for a binary relation over an unordered triple.
       (Contributed by Scott Fenton, 8-Jun-2011.) $)
    brtp $p |- ( X { <. A , B >. , <. C , D >. , <. E , F >. } Y <->
                 ( ( X = A /\ Y = B ) \/
                   ( X = C /\ Y = D ) \/
                   ( X = E /\ Y = F ) ) ) $=
      ( cop ctp wbr wceq w3o wa opth wcel df-br opex eltp 3orbi123i 3bitri ) GH
      ABNZCDNZEFNZOZPGHNZUJUAUKUGQZUKUHQZUKUIQZRGAQHBQSZGCQHDQSZGEQHFQSZRGHUJUB
      UKUGUHUIGHUCUDULUOUMUPUNUQGHABIJKTGHCDIJLTGHEFIJMTUEUF $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30.  (Contributed by Scott Fenton, 22-Apr-2011.) $)
    frirrc $p |- ( ( R Fr A /\ X e. A ) -> -. X R X ) $=
      ( vx wfr wcel wbr wn cv wa wi wceq eleq1 anbi2d breq1 breq2 bitrd imbi12d
      notbid frirr vtoclg anabsi7 ) ABEZCAFZCCBGZHZUCDIZAFZJZUGUGBGZHZKUCUDJZUF
      KDCAUGCLZUIULUKUFUMUHUDUCUGCAMNUMUJUEUMUJCUGBGUEUGCUGBOUGCCBPQSRDABTUAUB
      $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d A x y $.
    dftr6.1 $e |- A e. _V $.
    $( A potential definition of transitivity for sets.  (Contributed by Scott
       Fenton, 18-Mar-2012.) $)
    dftr6 $p |- ( Tr A <-> A e. ( _V \ ran ( ( _E o. _E ) \ _E ) ) ) $=
      ( vx vy wel cv wcel wa wi wal cep cdif cvv wbr wex epelcNEW anbi12i exbii
      wn 3bitri ccom crn wtr elrn brdif brco bitri notbii 19.41v exanali bitr3i
      vex epel exnal con2bii dftr2 eldif mpbiran 3bitr4i ) CDEZDFZAGZHZCFZAGZID
      JZCJZAKKUAZKLZUBZGZSZAUCAMVJLGZVKVGVKVDAVINZCOVFSZCOVGSCAVIBUDVNVOCVNVDAV
      HNZVDAKNZSZHVCDOZVESZHZVOVDAVHKUEVPVSVRVTVPVDVAKNZVAAKNZHZDOVSDVDAKKCULBU
      FWDVCDWBUTWCVBCDUMVAABPQRUGVQVEVDABPUHQWAVCVTHDOVOVCVTDUIVCVEDUJUKTRVFCUN
      TUOCDAUPVMAMGVLAMVJUQBURUS $.
      $( [22-Mar-2012] $) $( [18-Mar-2012] $)
  $}

  ${
    $d A x $.  $d B x $.  $d R x $.
    coep.1 $e |- A e. _V $.
    coep.2 $e |- B e. _V $.
    $( Composition with epsilon.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    coep $p |- ( A ( _E o. R ) B <-> E. x e. B A R x ) $=
      ( cv wbr cep wa wex wcel ccom wrex epelcNEW anbi2i ancom bitri exbii brco
      df-rex 3bitr4i ) BAGZDHZUCCIHZJZAKUCCLZUDJZAKBCIDMHUDACNUFUHAUFUDUGJUHUEU
      GUDUCCFOPUDUGQRSABCIDEFTUDACUAUB $.
      $( [18-Feb-2013] $)

    $( Composition with the converse of epsilon.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    coepr $p |- ( A ( R o. `' _E ) B <-> E. x e. A x R B ) $=
      ( cv cep ccnv wbr wa wex wcel ccom wrex vex brcnv epelcNEW bitri anbi1i
      exbii brco df-rex 3bitr4i ) BAGZHIZJZUECDJZKZALUEBMZUHKZALBCDUFNJUHABOUIU
      KAUGUJUHUGUEBHJUJBUEHEAPQUEBERSTUAABCDUFEFUBUHABUCUD $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( A quantifier free definition of a founded relationship.  (Contributed by
       Scott Fenton, 11-Apr-2011.) $)
    dffr5 $p |- ( R Fr A <->
                  ( ~P A \ { (/) } ) C_ ran ( _E \ ( _E o. `' R ) ) ) $=
      ( vx vz vy cv c0 cdif wcel cep wi wal wss wa wbr wrex vex anbi12i bitri
      wn cpw csn ccnv ccom crn wne wral wfr eldif elpw necon3bbii wex wel brdif
      elsn epel coep brcnv rexbii dfrex2 3bitrri con1bii df-rex 3bitr4i imbi12i
      exbii elrn albii dfss2 df-fr 3bitr4ri ) CFZAUAZGUBZHZIZVLJJBUCZUDZHZUEZIZ
      KZCLVLAMZVLGUFZNZDFZEFZBOZTDVLUGZEVLPZKZCLVOVTMABUHWBWKCVPWEWAWJVPVLVMIZV
      LVNIZTZNWEVLVMVNUIWLWCWNWDVLACQZUJWMVLGCGUOUKRSWGVLVSOZEULECUMZWINZEULWAW
      JWPWREWPWGVLJOZWGVLVROZTZNWRWGVLJVRUNWSWQXAWIECUPWIWTWTWGWFVQOZDVLPWHDVLP
      WITDWGVLVQEQZWOUQXBWHDVLWGWFBXCDQURUSWHDVLUTVAVBRSVFEVLVSWOVGWIEVLVCVDVEV
      HCVOVTVICEDABVJVK $.
      $( [11-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.
    $( Quantifier free definition of a strict order.  (Contributed by Scott
       Fenton, 22-Feb-2013.) $)
    dfso2 $p |- ( R Or A <->
              ( R Po A /\ ( A X. A ) C_ ( R u. ( _I u. `' R ) ) ) ) $=
      ( vx vy wor wpo cv wbr wral wa cid cun wcel wi wal wo brun bitr2i 3bitr4i
      vex wceq w3o cxp ccnv wss df-so cop opelxp ideq brcnv orbi2i 3orass df-br
      orbi12i imbi12i 2albii wrel wb relxp ssrel ax-mp r2al anbi2i bitr4i ) ABE
      ABFZCGZDGZBHZVFVGUAZVGVFBHZUBZDAICAIZJVEAAUCZBKBUDZLZLZUEZJCDABUFVQVLVEVF
      VGUGZVMMZVRVPMZNZDOCOZVFAMVGAMJZVKNZDOCOVQVLWAWDCDVSWCVTVKVFVGAADTZUHVKVF
      VGVPHZVTVHVIVJPZPVHVFVGVOHZPVKWFWGWHVHWHVFVGKHZVFVGVNHZPWGVFVGKVNQWIVIWJV
      JVFVGWEUIVFVGBCTWEUJUNRUKVHVIVJULVFVGBVOQSVFVGVPUMRUOUPVMUQVQWBURAAUSCDVM
      VPUTVAVKCDAAVBSVCVD $.
      $( [22-Feb-2013] $)
  $}

  ${
    $d R x y z w $.  $d A x y z w $.
    $( Quantifier free definition of a partial ordering.  (Contributed by Scott
       Fenton, 22-Feb-2013.) $)
    dfpo2 $p |- ( R Po A <-> ( ( R i^i ( _I |` A ) ) = (/) /\
           ( ( R i^i ( A X. A ) ) o. ( R i^i ( A X. A ) ) ) C_ R ) ) $=
      ( vx vy vz vw cid cin c0 wceq wa wb wbr wn wi wral wcel wal eleq1 df-br
      wpo cres cxp ccom wss po0 res0 ineq2i in0 eqtri xp0 coeq2i eqsstri pm3.2i
      0ss poeq2 reseq2 ineq2d eqeq1d xpeq2 coeq2d sseq1d anbi12d bibi12d mpbiri
      co02 2th wne r19.28zv ralbidv bitrd r19.26 syl6bb df-po disj opex syl6bbr
      cop vex brres cvv ididg ax-mp mpbiran bitr3i notbid imbi12d cla4v alrimiv
      con2d wex wrel relres elrel mpan ancri weq breq12 anidms a4v breq2 imbi2d
      cv biimpcd imp3a syl ideq anbi1i 3bitr3ri syl5ibrcom exlimdvv syl5 impbii
      df-ral 3bitr4i bitri wrex r2al impexp 2albii bitr4i ralcom r19.23v ralbii
      relco ssrel brin anbi12i ancom brxp anandi 3bitri anass exbii brco df-rex
      an4 r19.42v imbi12i 3bitr4ri 3bitr4g pm2.61ine ) ABUAZBGAUBZHZIJZBAAUCZHZ
      UUHUDZBUEZKZLZAIAIJZUULIBUAZBGIUBZHZIJZUUHBAIUCZHZUDZBUEZKZLUUNUVBBUFUUQU
      VAUUPBIHZIUUOIBGUGUHBUIZUJUUTIBUUTUUHIUDIUUSIUUHUUSUVCIUURIBAUKUHUVDUJULU
      UHVFUJBUOUMUNVGUUMUUCUUNUUKUVBAIBUPUUMUUFUUQUUJUVAUUMUUEUUPIUUMUUDUUOBAIG
      UQURUSUUMUUIUUTBUUMUUHUUSUUHUUMUUGUURBAIAUTURVAVBVCVDVEAIVHZCXCZUVFBMZNZU
      VFDXCZBMZUVIEXCZBMZKZUVFUVKBMZOZKEAPZDAPZCAPZUVHCAPZUVOEAPZDAPZCAPZKZUUCU
      UKUVEUVRUVHUWAKZCAPUWCUVEUVQUWDCAUVEUVQUVHUVTKZDAPUWDUVEUVPUWEDAUVHUVOEAV
      IVJUVHUVTDAVIVKVJUVHUWACAVLVMCDEABVNUUFUVSUUJUWBUUFFXCZUUDQZNZFBPZUVSFBUU
      DVOUWFBQZUWHOZFRZUVFAQZUVHOZCRZUWIUVSUWLUWOUWLUWNCUWLUVGUWMUWKUVGUWMNZOFU
      VFUVFVRZUVFUVFVPUWFUWQJZUWJUVGUWHUWPUWRUWJUWQBQUVGUWFUWQBSUVFUVFBTVQUWRUW
      GUWMUWRUWGUWQUUDQZUWMUWFUWQUUDSUWMUVFUVFUUDMZUWSUWTUVFUVFGMZUWMUVFUVFGACV
      SZVTUVFWAQUXAUXBUVFWAWBWCWDUVFUVFUUDTWEVQWFWGWHWJWIUWOUWKFUWOUWGUWJUWGUWF
      UVIUVKVRZJZEWKDWKZUWGKUWOUWJNZUWGUXEUUDWLUWGUXEGAWMDEUWFUUDWNWOWPUWOUXEUW
      GUXFUWOUXDUWGUXFOZDEUWOUXGUXDDEWQZUVIAQZKZUVLNZOZUWOUXIUVIUVIBMZNZOZUXLUW
      NUXOCDCDWQZUWMUXIUVHUXNUVFUVIASUXPUVGUXMUXPUVGUXMLUVFUVIUVFUVIBWRWSWFWGWT
      UXOUXHUXIUXKUXHUXOUXIUXKOUXHUXNUXKUXIUXHUXMUVLUVIUVKUVIBXAWFXBXDXEXFUXDUW
      GUXJUXFUXKUXDUWGUXCUUDQZUXJUWFUXCUUDSUVIUVKUUDMUVIUVKGMZUXIKUXQUXJUVIUVKG
      AEVSZVTUVIUVKUUDTUXRUXHUXIUVIUVKUXSXGXHXIVQUXDUWJUVLUXDUWJUXCBQUVLUWFUXCB
      SUVIUVKBTVQWFWGXJXKXEXLWJWIXMUWHFBXNUVHCAXNXOXPUVMDAXQZUVNOZEAPZCAPZUWMUV
      KAQZKZUXTKZUVNOZERCRZUWBUUJUYCUYEUYAOZERCRUYHUYACEAAXRUYGUYICEUYEUXTUVNXS
      XTYAUWAUYBCAUWAUVODAPZEAPUYBUVODEAAYBUYJUYAEAUVMUVNDAYCYDXPYDUUJUVFUVKVRZ
      UUIQZUYKBQZOZERCRZUYHUUIWLUUJUYOLUUHUUHYECEUUIBYFWCUYGUYNCEUYFUYLUVNUYMUV
      FUVIUUHMZUVIUVKUUHMZKZDWKZUXIUYEUVMKZKZDWKZUYLUYFUYRVUADUYRUVJUVFUVIUUGMZ
      KZUVLUVIUVKUUGMZKZKZUXIUYEKZUVMKZVUAUYPVUDUYQVUFUVFUVIBUUGYGUVIUVKBUUGYGY
      HVUGUVMVUCVUEKZKVUJUVMKVUIUVJVUCUVLVUEYQUVMVUJYIVUJVUHUVMUWMUXIKZUXIUYDKZ
      KUXIUWMKZVULKVUJVUHVUKVUMVULUWMUXIYIXHVUCVUKVUEVULUVFUVIAADVSYJUVIUVKAAUX
      SYJYHUXIUWMUYDYKXOXHYLUXIUYEUVMYMYLYNUYSUVFUVKUUIMUYLDUVFUVKUUHUUHUXBUXSY
      OUVFUVKUUITWEVUBUYTDAXQUYFUYTDAYPUYEUVMDAYRWEXIUVFUVKBTYSXTYAYTYHUUAUUB
      $.
      $( [22-Feb-2013] $)
  $}

  ${
    $d A a b c d e f g h p q x $.  $d a ps $.  $d a rh $.
    $d B a b c d e f g h p q x $.  $d b ch $.  $d b rh $.
    $d C a b c d e f g h p q x $.  $d c th $.  $d c rh $.
    $d D a b c d e f g h p q x $.  $d d ta $.  $d d rh $.
    $d E a b c d e f g h p q x $.  $d e et $.  $d e rh $.
    $d F a b c d e f g h p q x $.  $d f ze $.  $d f rh $.
    $d G a b c d e f g h p q x $.  $d g si $.  $d g rh $.
    $d H a b c d e f g h p q x $.  $d h rh $.  $d P a b c d e f g h p q $.
    $d p ph $.  $d q ph $.  $d Q a b c d e f g h x $.
    $d S a b c d e f g h p q x $.  $d X a b c d e f g h x $.  $d x ph $.
    $d x rh $.
    br8.1 $e |- ( a = A -> ( ph <-> ps ) ) $.
    br8.2 $e |- ( b = B -> ( ps <-> ch ) ) $.
    br8.3 $e |- ( c = C -> ( ch <-> th ) ) $.
    br8.4 $e |- ( d = D -> ( th <-> ta ) ) $.
    br8.5 $e |- ( e = E -> ( ta <-> et ) ) $.
    br8.6 $e |- ( f = F -> ( et <-> ze ) ) $.
    br8.7 $e |- ( g = G -> ( ze <-> si ) ) $.
    br8.8 $e |- ( h = H -> ( si <-> rh ) ) $.
    br8.9 $e |- ( x = X -> P = Q ) $.
    br8.10 $e |- R = { <. p , q >. |
       E. x e. S E. a e. P E. b e. P E. c e. P E. d e. P E. e e. P
       E. f e. P E. g e. P E. h e. P ( p = <. <. a , b >. , <. c , d >. >. /\
       q = <. <. e , f >. , <. g , h >. >. /\ ph ) } $.
    $( Substitution for an eight-place predicate.  (Contributed by Scott
       Fenton, 26-Sep-2013.) $)
    br8 $p |- ( ( ( X e. S /\ A e. Q /\ B e. Q ) /\
                   ( C e. Q /\ D e. Q /\ E e. Q ) /\
                   ( F e. Q /\ G e. Q /\ H e. Q ) ) ->
      ( <. <. A , B >. , <. C , D >. >. R <. <. E , F >. , <. G , H >. >. <->
        rh ) ) $=
      ( cop wbr wceq wrex wcel opex eqeq1 3anbi1d rexbidv 2rexbidv 3anbi2d brab
      cv w3a wi wa eqcom opth bitri anbi12i simp13 vex opthg syl simp22 anbi12d
      wb simp31 simp33 syl5bb ad3antrrr simpllr simplrl simplrr simprll simprlr
      bitrd simprrl simprrr 3bitrd biimpd exp3a rexlimdvw a1d rexlimdvv simpl11
      3impd simpl12 simpl13 simpl21 simpl22 simpl23 simpl31 simpl32 eqidd simpr
      syl6bi simpl33 opeq1 opeq2d 3anbi23d rcla42ev syl113anc 3anbi13d rcla43ev
      eqeq2d opeq2 opeq1d syl31anc rexeqdv rexeqbidv rcla4ev syl2anc ex impbid
      ) KLVDZMNVDZVDZUCUDVDZUEUFVDZVDZQVEUUAUJVPZUKVPZVDZULVPZUMVPZVDZVDZVFZUUD
      SVPZTVPZVDZUAVPZUBVPZVDZVDZVFZAVQZUBOVGZUAOVGZTOVGZSOVGZUMOVGZULOVGZUKOVG
      ZUJOVGZJRVGZUGRVHZKPVHZLPVHZVQZMPVHZNPVHZUCPVHZVQZUDPVHZUEPVHZUFPVHZVQZVQ
      ZIUIVPZUUKVFZUHVPZUUSVFZAVQZUBOVGZUAOVGTOVGZSOVGUMOVGZULOVGUKOVGZUJOVGJRV
      GUULUWGAVQZUBOVGZUAOVGTOVGZSOVGUMOVGZULOVGUKOVGZUJOVGJRVGUVJUIUHUUAUUDQYS
      YTVIUUBUUCVIUWDUUAVFZUWLUWQJUJROUWRUWKUWPUKULOOUWRUWJUWOUMSOOUWRUWIUWNTUA
      OOUWRUWHUWMUBOUWRUWEUULUWGAUWDUUAUUKVJVKVLVMVMVMVMUWFUUDVFZUWQUVHJUJROUWS
      UWPUVFUKULOOUWSUWOUVDUMSOOUWSUWNUVBTUAOOUWSUWMUVAUBOUWSUWGUUTUULAUWFUUDUU
      SVJVNVLVMVMVMVMVCVOUWCUVJIUWCUVHIJUJROUWCUVHIVRJVPZRVHUUEOVHVSUWCUVFIUKUL
      OOUWCUVFIVRUUFOVHUUHOVHVSUWCUVDIUMSOOUWCUVDIVRUUIOVHUUMOVHVSUWCUVBITUAOOU
      WCUVBIVRUUNOVHUUPOVHVSUWCUVAIUBOUWCUULUUTAIUWCUULUUTAIVRZUWCUULUUTVSZUUEK
      VFZUUFLVFZVSZUUHMVFZUUINVFZVSZVSZUUMUCVFZUUNUDVFZVSZUUPUEVFZUUQUFVFZVSZVS
      ZVSZUXAUXBUUGYSVFZUUJYTVFZVSZUUOUUBVFZUURUUCVFZVSZVSUWCUXQUULUXTUUTUYCUUL
      UUKUUAVFUXTUUAUUKVTUUGUUJYSYTUUEUUFVIUUHUUIVIMNVIWAWBUUTUUSUUDVFUYCUUDUUS
      VTUUOUURUUBUUCUUMUUNVIUUPUUQVIUEUFVIWAWBWCUWCUXTUXIUYCUXPUWCUXRUXEUXSUXHU
      WCUVMUXRUXEWJUVKUVLUVMUVRUWBWDUUEUUFKLPUJWEUKWEWFWGUWCUVPUXSUXHWJUVNUVOUV
      PUVQUWBWHUUHUUIMNPULWEUMWEWFWGWIUWCUYAUXLUYBUXOUWCUVSUYAUXLWJUVNUVRUVSUVT
      UWAWKUUMUUNUCUDPSWETWEWFWGUWCUWAUYBUXOWJUVNUVRUVSUVTUWAWLUUPUUQUEUFPUAWEU
      BWEWFWGWIWIWMUXQAIUXQABCIUXCABWJUXDUXHUXPUNWNUXQUXDBCWJUXCUXDUXHUXPWOUOWG
      UXQCDFIUXQUXFCDWJUXEUXFUXGUXPWPUPWGUXQDEFUXQUXGDEWJUXEUXFUXGUXPWQUQWGUXQU
      XJEFWJUXIUXJUXKUXOWRURWGWTUXQFGHIUXQUXKFGWJUXIUXJUXKUXOWSUSWGUXQUXMGHWJUX
      IUXLUXMUXNXAUTWGUXQUXNHIWJUXIUXLUXMUXNXBVAWGXCXCXCXDXTXEXJXFXGXHXGXHXGXHX
      GXHUWCIUVJUWCIVSZUVKUVAUBPVGZUAPVGZTPVGZSPVGZUMPVGZULPVGZUKPVGZUJPVGZUVJU
      VKUVLUVMUVRUWBIXIUYDUVLUVMUVOUUAYSMUUIVDZVDZVFZUUTDVQZUBPVGZUAPVGZTPVGZSP
      VGUMPVGZUYLUVKUVLUVMUVRUWBIXKUVKUVLUVMUVRUWBIXLUVOUVPUVQUVNUWBIXMUYDUVPUV
      QUVSUUAUUAVFZUUDUUBUURVDZVFZGVQZUBPVGUAPVGZUYTUVOUVPUVQUVNUWBIXNUVOUVPUVQ
      UVNUWBIXOUVSUVTUWAUVNUVRIXPUYDUVTUWAVUAUUDUUDVFZIVUEUVSUVTUWAUVNUVRIXQUVS
      UVTUWAUVNUVRIYAUYDUUAXRUYDUUDXRUWCIXSVUDVUAVUFIVQVUAUUDUUBUEUUQVDZVDZVFZH
      VQUAUBUEUFPPUXMVUCVUIGHVUAUXMVUBVUHUUDUXMUURVUGUUBUUPUEUUQYBYCYIUTYDUXNVU
      IVUFHIVUAUXNVUHUUDUUDUXNVUGUUCUUBUUQUFUEYJYCYIVAYDYEYFUYRVUEVUAUUTEVQZUBP
      VGUAPVGVUAUUDUCUUNVDZUURVDZVFZFVQZUBPVGUAPVGUMSTNUCUDPPPUXGUYPVUJUAUBPPUX
      GUYOVUADEUUTUXGUYNUUAUUAUXGUYMYTYSUUINMYJYCYIUQYGVMUXJVUJVUNUAUBPPUXJUUTV
      UMEFVUAUXJUUSVULUUDUXJUUOVUKUURUUMUCUUNYBYKYIURYDVMUXKVUNVUDUAUBPPUXKVUMV
      UCFGVUAUXKVULVUBUUDUXKVUKUUBUURUUNUDUCYJYKYIUSYDVMYHYLUYIUYTUUAKUUFVDZUUJ
      VDZVFZUUTBVQZUBPVGZUAPVGTPVGZSPVGUMPVGUUAYSUUJVDZVFZUUTCVQZUBPVGZUAPVGTPV
      GZSPVGUMPVGUJUKULKLMPPPUXCUYGVUTUMSPPUXCUYEVUSTUAPPUXCUVAVURUBPUXCUULVUQA
      BUUTUXCUUKVUPUUAUXCUUGVUOUUJUUEKUUFYBYKYIUNYGVLVMVMUXDVUTVVEUMSPPUXDVUSVV
      DTUAPPUXDVURVVCUBPUXDVUQVVBBCUUTUXDVUPVVAUUAUXDVUOYSUUJUUFLKYJYKYIUOYGVLV
      MVMUXFVVEUYSUMSPPUXFVVDUYQTUAPPUXFVVCUYPUBPUXFVVBUYOCDUUTUXFVVAUYNUUAUXFU
      UJUYMYSUUHMUUIYBYCYIUPYGVLVMVMYHYLUVIUYLJUGRUWTUGVFZUVHUYKUJOPVBVVFUVGUYJ
      UKOPVBVVFUVFUYIULOPVBVVFUVEUYHUMOPVBVVFUVDUYGSOPVBVVFUVCUYFTOPVBVVFUVBUYE
      UAOPVBVVFUVAUBOPVBYMYNYNYNYNYNYNYNYOYPYQYRWM $.
      $( [14-Oct-2013] $) $( [26-Sep-2013] $)
  $}

  ${
    $d A a b c d e f p q x $.  $d a ps $.  $d a ze $.
    $d B a b c d e f p q x $.  $d b ch $.  $d b ze $.
    $d C a b c d e f p q x $.  $d c th $.  $d c ze $.
    $d D a b c d e f p q x $.  $d d ta $.  $d d ze $.
    $d E a b c d e f p q x $.  $d e et $.  $d e ze $.
    $d F a b c d e f p q x $.  $d f ze $.  $d P a b c d e f p q $.
    $d p q x ph $.  $d Q a b c d e f p q x $.  $d S a b c d e f p q x $.
    $d X a b c d e f x $.  $d x ze $.
    br6.1 $e |- ( a = A -> ( ph <-> ps ) ) $.
    br6.2 $e |- ( b = B -> ( ps <-> ch ) ) $.
    br6.3 $e |- ( c = C -> ( ch <-> th ) ) $.
    br6.4 $e |- ( d = D -> ( th <-> ta ) ) $.
    br6.5 $e |- ( e = E -> ( ta <-> et ) ) $.
    br6.6 $e |- ( f = F -> ( et <-> ze ) ) $.
    br6.7 $e |- ( x = X -> P = Q ) $.
    br6.8 $e |- R = { <. p , q >. |
       E. x e. S E. a e. P E. b e. P E. c e. P E. d e. P E. e e. P
       E. f e. P ( p = <. a , <. b , c >. >. /\
       q = <. d , <. e , f >. >. /\ ph ) } $.
    $( Substitution for an six-place predicate.  (Contributed by Scott Fenton,
       4-Oct-2013.) $)
    br6 $p |- ( ( X e. S /\
                   ( A e. Q /\ B e. Q /\ C e. Q ) /\
                   ( D e. Q /\ E e. Q /\ F e. Q ) ) ->
      ( <. A , <. B , C >. >. R <. D , <. E , F >. >. <-> ze ) ) $=
      ( cop wbr cv wceq w3a wrex wcel opex eqeq1 eqcom 3anbi1d rexbidv 2rexbidv
      syl6bb 3anbi2d brab wi vex opth anbi12i simp23 simp33 opthg biimpa anim2i
      wa an12s sylan9bb biimpd syl2an an4s syl2anc syl5bi exp3a 3impd rexlimdvw
      ex rexlimdvv simpl1 simpl2 opeq1 eqeq1d 3anbi23d opeq2d opeq2 df-3an eqid
      pm3.2i mpbiran rcla43ev 3ad2antl3 rexeqdv rexeqbidv rcla4ev impbid syl5bb
      a1d 3anbi13d ) IJKUPZUPZLSTUPZUPZOUQUDURZUEURZUFURZUPZUPZXOUSZUGURZQURZRU
      RZUPZUPZXQUSZAUTZRMVAZQMVAZUGMVAZUFMVAZUEMVAZUDMVAZHPVAZUAPVBZINVBZJNVBZK
      NVBZUTZLNVBZSNVBZTNVBZUTZUTZGUCURZYBUSZUBURZYHUSZAUTZRMVAZQMVAUGMVAZUFMVA
      UEMVAZUDMVAHPVAYCUUKAUTZRMVAZQMVAUGMVAZUFMVAUEMVAZUDMVAHPVAYQUCUBXOXQOIXN
      VCLXPVCUUHXOUSZUUOUUSHUDPMUUTUUNUURUEUFMMUUTUUMUUQUGQMMUUTUULUUPRMUUTUUIY
      CUUKAUUTUUIXOYBUSYCUUHXOYBVDXOYBVEVIVFVGVHVHVHUUJXQUSZUUSYOHUDPMUVAUURYMU
      EUFMMUVAUUQYKUGQMMUVAUUPYJRMUVAUUKYIYCAUVAUUKXQYHUSYIUUJXQYHVDXQYHVEVIVJV
      GVHVHVHUOVKUUGYQGUUGYOGHUDPMUUGYOGVLHURZPVBXRMVBWAUUGYMGUEUFMMUUGYMGVLXSM
      VBXTMVBWAUUGYKGUGQMMUUGYKGVLYDMVBYEMVBWAUUGYJGRMUUGYCYIAGUUGYCYIAGVLZYCYI
      WAXRIUSZYAXNUSZWAZYDLUSZYGXPUSZWAZWAZUUGUVCYCUVFYIUVIXRYAIXNUDVMXSXTVCJKV
      CVNYDYGLXPUGVMYEYFVCSTVCVNVOUUGUUAUUEUVJUVCVLYRYSYTUUAUUFVPYRUUBUUCUUDUUE
      VQUUAUUEWAUVJUVCUUAUVFUUEUVIUVCUUAUVFWAUVDXSJUSZXTKUSZWAZWAZUVGYESUSZYFTU
      SZWAZWAZUVCUUEUVIWAUVDUUAUVEUVNUUAUVEWAUVMUVDUUAUVEUVMXSXTJKNUEVMUFVMVRVS
      VTWBUVGUUEUVHUVRUUEUVHWAUVQUVGUUEUVHUVQYEYFSTNQVMRVMVRVSVTWBUVNUVRWAAGUVN
      ADUVRGUVDABUVMDUHUVKBCUVLDUIUJWCWCUVGDEUVQGUKUVOEFUVPGULUMWCWCWCWDWEWFWLW
      GWHWIWJWKXLWMXLWMXLWMUUGGYQUUGGWAZYRYJRNVAZQNVAZUGNVAZUFNVAZUENVAZUDNVAZY
      QYRUUBUUFGWNUVSUUBXOXOUSZYIDUTZRNVAZQNVAUGNVAZUWEYRUUBUUFGWOUUFYRGUWIUUBU
      WGGUWFLYGUPZXQUSZEUTUWFLSYFUPZUPZXQUSZFUTZUGQRLSTNNNUVGYIUWKDEUWFUVGYHUWJ
      XQYDLYGWPWQUKWRUVOUWKUWNEFUWFUVOUWJUWMXQUVOYGUWLLYESYFWPWSWQULWRUVPUWOUWF
      XQXQUSZGUTZGUVPUWNUWPFGUWFUVPUWMXQXQUVPUWLXPLYFTSWTWSWQUMWRUWQUWFUWPWAGUW
      FUWPGXAUWFUWPXOXBXQXBXCXDVIXEXFUWBUWIIYAUPZXOUSZYIBUTZRNVAZQNVAUGNVAIJXTU
      PZUPZXOUSZYICUTZRNVAZQNVAUGNVAUDUEUFIJKNNNUVDUVTUXAUGQNNUVDYJUWTRNUVDYCUW
      SABYIUVDYBUWRXOXRIYAWPWQUHXMVGVHUVKUXAUXFUGQNNUVKUWTUXERNUVKUWSUXDBCYIUVK
      UWRUXCXOUVKYAUXBIXSJXTWPWSWQUIXMVGVHUVLUXFUWHUGQNNUVLUXEUWGRNUVLUXDUWFCDY
      IUVLUXCXOXOUVLUXBXNIXTKJWTWSWQUJXMVGVHXEWGYPUWEHUAPUVBUAUSZYOUWDUDMNUNUXG
      YNUWCUEMNUNUXGYMUWBUFMNUNUXGYLUWAUGMNUNUXGYKUVTQMNUNUXGYJRMNUNXGXHXHXHXHX
      HXIWGWLXJXK $.
      $( [14-Oct-2013] $) $( [4-Oct-2013] $)
  $}

  ${
    $d A a $.  $d A b $.  $d A c $.  $d A d $.  $d a p $.  $d A p $.  $d a q $.
    $d A q $.  $d A x $.  $d B a $.  $d B b $.  $d B c $.  $d B d $.  $d b p $.
    $d B p $.  $d b q $.  $d B q $.  $d B x $.  $d C a $.  $d C b $.  $d C c $.
    $d C d $.  $d c p $.  $d C p $.  $d c q $.  $d C q $.  $d C x $.  $d D a $.
    $d D b $.  $d D c $.  $d D d $.  $d d p $.  $d D p $.  $d d q $.  $d D q $.
    $d D x $.  $d P p $.  $d p ph $.  $d p q $.  $d P q $.  $d p x $.
    $d ph q $.  $d q x $.  $d S p $.  $d S q $.  $d a b $.  $d a c $.
    $d a d $.  $d a ps $.  $d a ta $.  $d a x $.  $d b c $.  $d b ch $.
    $d b d $.  $d b ta $.  $d b x $.  $d c d $.  $d c ta $.  $d c th $.
    $d c x $.  $d d ta $.  $d d x $.  $d P a $.  $d P b $.  $d P c $.
    $d P d $.  $d ph x $.  $d Q a $.  $d Q b $.  $d Q c $.  $d Q d $.
    $d Q x $.  $d S a $.  $d S x $.  $d ta x $.  $d X a $.  $d X b $.
    $d X c $.  $d X x $.
    br4.1 $e |- ( a = A -> ( ph <-> ps ) ) $.
    br4.2 $e |- ( b = B -> ( ps <-> ch ) ) $.
    br4.3 $e |- ( c = C -> ( ch <-> th ) ) $.
    br4.4 $e |- ( d = D -> ( th <-> ta ) ) $.
    br4.5 $e |- ( x = X -> P = Q ) $.
    br4.6 $e |- R = { <. p , q >. |
       E. x e. S E. a e. P E. b e. P E. c e. P E. d e. P
       ( p = <. a , b >. /\ q = <. c , d >. /\ ph ) } $.
    $( Substitution for a four-place predicate.  (Contributed by Scott Fenton,
       9-Oct-2013.) $)
    br4 $p |- ( ( X e. S /\
                   ( A e. Q /\ B e. Q ) /\
                   ( C e. Q /\ D e. Q ) ) ->
      ( <. A , B >. R <. C , D >. <-> ta ) ) $=
      ( cop wbr cv wceq w3a wrex wcel wa eqeq1 3anbi1d rexbidv 2rexbidv 3anbi2d
      opex brab wi simp2r simp3r eqcom anbi12i vex opthg bi2anan9 syl5bb biimpd
      syl6bi exp3a 3impd rexlimdvw a1d rexlimdvv syl2anc simpl1 simpl2l simpl2r
      sylan9bb simpl3l simpl3r eqidd simpr opeq1 eqeq2d 3anbi23d opeq2 rcla42ev
      syl113anc 3anbi13d syl3anc rexeqdv rexeqbidv rcla4ev ex impbid ) GHUHZIJU
      HZMUIXARUJZSUJZUHZUKZXBTUJZUAUJZUHZUKZAULZUAKUMZTKUMZSKUMZRKUMZFNUMZONUNZ
      GLUNZHLUNZUOZILUNZJLUNZUOZULZEQUJZXEUKZPUJZXIUKZAULZUAKUMZTKUMSKUMZRKUMFN
      UMXFYHAULZUAKUMZTKUMSKUMZRKUMFNUMXPQPXAXBMGHVAIJVAYEXAUKZYKYNFRNKYOYJYMST
      KKYOYIYLUAKYOYFXFYHAYEXAXEUPUQURUSUSYGXBUKZYNXNFRNKYPYMXLSTKKYPYLXKUAKYPY
      HXJXFAYGXBXIUPUTURUSUSUGVBYDXPEYDXSYBXPEVCXQXRXSYCVDXQXTYAYBVEXSYBUOZXNEF
      RNKYQXNEVCFUJZNUNXCKUNUOYQXLESTKKYQXLEVCXDKUNXGKUNUOYQXKEUAKYQXFXJAEYQXFX
      JAEVCZYQXFXJUOZXCGUKZXDHUKZUOZXGIUKZXHJUKZUOZUOZYSYTXEXAUKZXIXBUKZUOYQUUG
      XFUUHXJUUIXAXEVFXBXIVFVGXSUUHUUCYBUUIUUFXCXDGHLRVHSVHVIXGXHIJLTVHUAVHVIVJ
      VKUUGAEUUCACUUFEUUAABUUBCUBUCWCUUDCDUUEEUDUEWCWCVLVMVNVOVPVQVRVQVRVSYDEXP
      YDEUOZXQXKUALUMZTLUMZSLUMZRLUMZXPXQXTYCEVTUUJXRXSXAXAUKZXJCULZUALUMTLUMZU
      UNXRXSXQYCEWAXRXSXQYCEWBUUJYAYBUUOXBXBUKZEUUQYAYBXQXTEWDYAYBXQXTEWEUUJXAW
      FUUJXBWFYDEWGUUPUUOUUREULUUOXBIXHUHZUKZDULTUAIJLLUUDXJUUTCDUUOUUDXIUUSXBX
      GIXHWHWIUDWJUUEUUTUURDEUUOUUEUUSXBXBXHJIWKWIUEWJWLWMUULUUQXAGXDUHZUKZXJBU
      LZUALUMTLUMRSGHLLUUAXKUVCTUALLUUAXFUVBABXJUUAXEUVAXAXCGXDWHWIUBWNUSUUBUVC
      UUPTUALLUUBUVBUUOBCXJUUBUVAXAXAXDHGWKWIUCWNUSWLWOXOUUNFONYROUKZXNUUMRKLUF
      UVDXMUULSKLUFUVDXLUUKTKLUFUVDXKUAKLUFWPWQWQWQWRVSWSWTVK $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( Alternate definition of restriction.  (Contributed by Scott Fenton,
       17-Apr-2014.) $)
    dfres3 $p |- ( A |` B ) = ( A i^i ( B X. ran A ) ) $=
      ( vx vy vz cres cvv cxp cin crn df-res cv wcel cop wceq wex vex elxp elin
      wa wb eleq1 biantru opelrn biantrud syl5bbr com12 pm5.32d 2exbidv 3bitr4g
      syl6bi pm5.32i 3bitr4i eqriv eqtri ) ABFABGHZIZABAJZHZIZABKCUQUTCLZAMZVAU
      PMZTVBVAUSMZTVAUQMVAUTMVBVCVDVBVADLZELZNZOZVEBMZVFGMZTZTZEPDPVHVIVFURMZTZ
      TZEPDPVCVDVBVLVODEVBVHVKVNVHVBVKVNUAZVHVBVGAMZVPVAVGAUBVKVIVQVNVJVIEQZUCV
      QVMVIVEVFADQVRUDUEUFUKUGUHUIDEVABGRDEVABURRUJULVAAUPSVAAUSSUMUNUO $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( Another distributive law of converse over class composition.
       (Contributed by Scott Fenton, 3-May-2014.) $)
    cnvco1 $p |- `' ( `' A o. B ) = ( `' B o. A ) $=
      ( vx vy vz ccnv ccom relcnv relco cv wbr wa wex cop ancom vex brcnv bitri
      wcel opelco bicomi anbi12i exbii opelcnv 3bitr4i eqrelriiv ) CDAFZBGZFZBF
      ZAGZUHHUJAIDJZEJZBKZUMCJZUGKZLZEMZUOUMAKZUMULUJKZLZEMUOULNZUISZVBUKSUQVAE
      UQUPUNLVAUNUPOUPUSUNUTUMUOAEPZCPZQUTUNUMULBVDDPZQUAUBRUCVCULUONUHSURUOULU
      HVEVFUDEULUOUGBVFVETREUOULUJAVEVFTUEUF $.
      $( [3-May-2014] $)

    $( Another distributive law of converse over class composition.
       (Contributed by Scott Fenton, 3-May-2014.) $)
    cnvco2 $p |- `' ( A o. `' B ) = ( B o. `' A ) $=
      ( vx vy vz ccnv ccom relcnv relco cv wbr wa wex cop ancom vex brcnv bitri
      wcel opelco bicomi anbi12i exbii opelcnv 3bitr4i eqrelriiv ) CDABFZGZFZBA
      FZGZUHHBUJIDJZEJZUGKZUMCJZAKZLZEMZUOUMUJKZUMULBKZLZEMUOULNZUISZVBUKSUQVAE
      UQUPUNLVAUNUPOUPUSUNUTUSUPUOUMACPZEPZQUAULUMBDPZVEQUBRUCVCULUONUHSURUOULU
      HVDVFUDEULUOAUGVFVDTREUOULBUJVDVFTUEUF $.
      $( [3-May-2014] $)
  $}


