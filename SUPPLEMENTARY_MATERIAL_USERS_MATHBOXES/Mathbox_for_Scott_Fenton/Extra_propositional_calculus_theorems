$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Extra propositional calculus theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Partial elimination of a triple disjunction by denial of a disjunct.
     (Contributed by Scott Fenton, 26-Mar-2011.) $)
  3orel1 $p |- ( -. ph -> ( ( ph \/ ps \/ ch ) -> ( ps \/ ch ) ) ) $=
    ( w3o wo wn 3orass orel1 syl5bi ) ABCDABCEZEAFJABCGAJHI $.
    $( [26-Mar-2011] $)

  $( Partial elimination of a triple disjunction by denial of a disjunct.  (The
     proof was shortened by Andrew Salmon, 25-May-2011.)  (Contributed by Scott
     Fenton, 26-Mar-2011.) $)
  3orel2 $p |- ( -. ps -> ( ( ph \/ ps \/ ch ) -> ( ph \/ ch ) ) ) $=
    ( w3o wn wo 3orrot 3orel1 orcom syl6ib syl5bi ) ABCDBCADZBEZACFZABCGMLCAFNB
    CAHCAIJK $.
    $( [25-May-2011] $) $( [26-Mar-2011] $)

  $( Partial elimination of a triple disjunction by denial of a disjunct.
     (Contributed by Scott Fenton, 26-Mar-2011.) $)
  3orel3 $p |- ( -. ch -> ( ( ph \/ ps \/ ch ) -> ( ph \/ ps ) ) ) $=
    ( w3o wo wn df-3or orel2 syl5bi ) ABCDABEZCECFJABCGCJHI $.
    $( [26-Mar-2011] $)

  ${
    3pm3.2ni.1 $e |- -. ph $.
    3pm3.2ni.2 $e |- -. ps $.
    3pm3.2ni.3 $e |- -. ch $.
    $( Triple negated disjuntion introduction.  (Contributed by Scott Fenton,
       20-Apr-2011.) $)
    3pm3.2ni $p |- -. ( ph \/ ps \/ ch ) $=
      ( w3o wo pm3.2ni df-3or mtbir ) ABCGABHZCHLCABDEIFIABCJK $.
      $( [20-Apr-2011] $)
  $}

  ${
    3jaodd.1 $e |- ( ph -> ( ps -> ( ch -> et ) ) ) $.
    3jaodd.2 $e |- ( ph -> ( ps -> ( th -> et ) ) ) $.
    3jaodd.3 $e |- ( ph -> ( ps -> ( ta -> et ) ) ) $.
    $( Double deduction form of ~ 3jaoi .  (Contributed by Scott Fenton,
       20-Apr-2011.) $)
    3jaodd $p |- ( ph -> ( ps -> ( ( ch \/ th \/ ta ) -> et ) ) ) $=
      ( w3o wi com3r 3jaoi com3l ) CDEJABFCABFKKDEABCFGLABDFHLABEFILMN $.
      $( [20-Apr-2011] $)
  $}

  $( Closed form of ~ 3ori , (Contributed by Scott Fenton, 20-Apr-2011.) $)
  3orit $p |- ( ( ph \/ ps \/ ch ) <-> ( ( -. ph /\ -. ps ) -> ch ) ) $=
    ( w3o wo wn wi wa df-3or df-or ioran imbi1i 3bitri ) ABCDABEZCENFZCGAFBFHZC
    GABCINCJOPCABKLM $.
    $( [20-Apr-2011] $)

  ${
    3mixd.1 $e |- ( ph -> ps ) $.
    $( Deduction introducing triple disjunction.  (Contributed by Scott Fenton,
       8-Jun-2011.) $)
    3mix1d $p |- ( ph -> ( ps \/ ch \/ th ) ) $=
      ( w3o 3mix1 syl ) ABBCDFEBCDGH $.
      $( [8-Jun-2011] $)

    $( Deduction introducing triple disjunction.  (Contributed by Scott Fenton,
       8-Jun-2011.) $)
    3mix2d $p |- ( ph -> ( ch \/ ps \/ th ) ) $=
      ( w3o 3mix2 syl ) ABCBDFEBCDGH $.
      $( [8-Jun-2011] $)

    $( Deduction introducing triple disjunction.  (Contributed by Scott Fenton,
       8-Jun-2011.) $)
    3mix3d $p |- ( ph -> ( ch \/ th \/ ps ) ) $=
      ( w3o 3mix3 syl ) ABCDBFEBCDGH $.
      $( [8-Jun-2011] $)
  $}

  $( A biconditional in the antecedent is the same as two implications.
     (Contributed by Scott Fenton, 12-Dec-2010.) $)
  biimpexp $p |- ( ( ( ph <-> ps ) -> ch )
                   <-> ( ( ph -> ps ) -> ( ( ps -> ph ) -> ch ) ) ) $=
    ( wb wi wa dfbi2 imbi1i impexp bitri ) ABDZCEABEZBAEZFZCELMCEEKNCABGHLMCIJ
    $.
    $( [12-Dec-2010] $)

  $( Elimination of two disjuncts in a triple disjunction.  (Contributed by
     Scott Fenton, 9-Jun-2011.) $)
  3orel13 $p |- ( ( -. ph /\ -. ch ) -> ( ( ph \/ ps \/ ch ) -> ps ) ) $=
    ( wn w3o wo 3orel3 orel1 sylan9r ) CDABCEABFADBABCGABHI $.
    $( [9-Jun-2011] $)

