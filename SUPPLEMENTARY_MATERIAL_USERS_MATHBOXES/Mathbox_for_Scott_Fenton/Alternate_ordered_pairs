$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Alternate ordered pairs
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c << >> XX. $.
  $( Declare the syntax for an alternate ordered pair. $)
  caltop $a class << A , B >> $.
  $( Declare the syntax for an alternate cross product. $)
  caltxp $a class ( A XX. B ) $.

  $( An alternative definition of ordered pairs.  This definition removes a
     hypothesis from its defining theorem (see ~ altopth ), making it more
     convenient in some circumstances. $)
  df-altop $a |- << A , B >> = { { A } , { A , { B } } } $.

  ${
    $d A x y z $.  $d B x y z $.
    $( Define cross products of alternative ordered pairs. $)
    df-altxp $a |- ( A XX. B ) = { z | E. x e. A E. y e. B z = << x , y >> } $.
  $}

  $( Alternative ordered pairs always exist.  (Contributed by Scott Fenton,
     22-Mar-2012.) $)
  altopex $p |- << A , B >> e. _V $=
    ( caltop csn cpr cvv df-altop prex eqeltri ) ABCADZABDEZEFABGJKHI $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Two alternate ordered pairs are equal iff the singletons of their
     respective elements are equal.  Note that this holds regardless of sethood
     of any of the elements.  (Contributed by Scott Fenton, 16-Apr-2012.) $)
  altopthsn $p |- ( << A , B >> = << C , D >> <->
                    ( { A } = { C } /\ { B } = { D } ) ) $=
    ( caltop wceq csn cpr wa df-altop eqeq12i snex prex wss snsspr1 sseq2 df-pr
    cun preq2d preqr2 preq12b simpl mpbii adantl mpbiri adantr eqssd jaoi sylbi
    wo uneq1 3eqtr4g preq1 eqtrd eqeq1d biimpd syl syl6com jcai sylan9eq impbii
    preq2 bitri ) ABEZCDEZFAGZABGZHZHZCGZCDGZHZHZFZVFVJFZVGVKFZIZVDVIVEVMABJCDJ
    KVNVQVNVOVPVNVOVHVLFZIZVFVLFZVHVJFZIZUJVOVFVHVJVLALAVGMCLCVKMZUAVSVOWBVOVRU
    BWBVFVJWAVFVJNZVTWAVFVHNWDAVGOVHVJVFPUCUDVTVJVFNZWAVTWEVJVLNCVKOVFVLVJPUEUF
    UGUHUIVOVNVJCVGHZHZVMFZVPVOVNWHVOVIWGVMVOVIVFWFHWGVOVHWFVFVOVFVGGZRVJWIRVHW
    FVFVJWIUKAVGQCVGQULSVFVJWFUMUNZUOUPWHWFVLFVPWFVLVJCVGMWCTVGVKCBLDLTUQURUSVO
    VPVIWGVMWJVPWFVLVJVGVKCVBSUTVAVC $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  $( Equality for alternate ordered pairs.  (Contributed by Scott Fenton,
     22-Mar-2012.) $)
  altopeq12 $p |- ( ( A = B /\ C = D ) -> << A , C >> = << B , D >> ) $=
    ( wceq wa csn caltop sneq anim12i altopthsn sylibr ) ABEZCDEZFAGBGEZCGDGEZF
    ACHBDHEMONPABICDIJACBDKL $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Equality for alternate ordered pairs.  (Contributed by Scott Fenton,
     22-Mar-2012.) $)
  altopeq1 $p |- ( A = B -> << A , C >> = << B , C >> ) $=
    ( wceq caltop eqid altopeq12 mpan2 ) ABDCCDACEBCEDCFABCCGH $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Equality for alternate ordered pairs.  (Contributed by Scott Fenton,
     22-Mar-2012.) $)
  altopeq2 $p |- ( A = B -> << C , A >> = << C , B >> ) $=
    ( wceq caltop eqid altopeq12 mpan ) CCDABDCAECBEDCFCCABGH $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Equality of the first members of equal alternate ordered pairs, which
     holds regardless of the second members' sethood.  (Contributed by Scott
     Fenton, 22-Mar-2012.) $)
  altopth1 $p |- ( A e. V -> ( << A , B >> = << C , D >> -> A = C ) ) $=
    ( caltop wceq csn wa wcel altopthsn sneqrg adantrd syl5bi ) ABFCDFGAHCHGZBH
    DHGZIAEJZACGZABCDKQORPACELMN $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Equality of the second members of equal alternate ordered pairs, which
     holds regardless of the first members' sethood.  (Contributed by Scott
     Fenton, 22-Mar-2012.) $)
  altopth2 $p |- ( B e. V -> ( << A , B >> = << C , D >> -> B = D ) ) $=
    ( caltop wceq csn wa wcel altopthsn sneqrg adantld syl5bi ) ABFCDFGAHCHGZBH
    DHGZIBEJZBDGZABCDKQPROBDELMN $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Alternate ordered pair theorem.  (Contributed by Scott Fenton,
     22-Mar-2012.) $)
  altopthg $p |- ( ( A e. V /\ B e. W ) ->
                   ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) ) $=
    ( caltop wceq csn wa wcel altopthsn sneqbg bi2anan9 syl5bb ) ABGCDGHAICIHZB
    IDIHZJAEKZBFKZJACHZBDHZJABCDLRPTSQUAACEMBDFMNO $.
    $( [11-Apr-2012] $) $( [22-Mar-2012] $)

  $( Alternate ordered pair theorem.  (Contributed by Scott Fenton,
     14-Apr-2012.) $)
  altopthbg $p |- ( ( A e. V /\ D e. W ) ->
                   ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) ) $=
    ( caltop wceq csn wa wcel altopthsn sneqbg eqcom 3bitr4g bi2anan9 syl5bb )
    ABGCDGHAICIHZBIZDIZHZJAEKZDFKZJACHZBDHZJABCDLUBRUDUCUAUEACEMUCTSHDBHUAUEDBF
    MSTNBDNOPQ $.
    $( [12-May-2012] $) $( [14-Apr-2012] $)

  ${
    altopth.1 $e |- A e. _V $.
    altopth.2 $e |- B e. _V $.
    $( The alternate ordered pair theorem.  If two alternate ordered pairs are
       equal, their first elements are equal and their second elements are
       equal.  Note that ` C ` and ` D ` are not required to be a set due to a
       peculiarity of our specific ordered pair definition, as opposed to the
       regular ordered pairs used here, which (as in ~ opth ), requires ` D `
       to be a set.  (Contributed by Scott Fenton, 23-Mar-2012.) $)
    altopth $p |- ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) $=
      ( cvv wcel caltop wceq wa wb altopthg mp2an ) AGHBGHABICDIJACJBDJKLEFABCD
      GGMN $.
      $( [11-Apr-2012] $) $( [23-Mar-2012] $)
  $}

  ${
    altopthb.1 $e |- A e. _V $.
    altopthb.2 $e |- D e. _V $.
    $( Alternate ordered pair theorem with different sethood requirements.  See
       ~ altopth for more comments.  (Contributed by Scott Fenton,
       14-Apr-2012.) $)
    altopthb $p |- ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) $=
      ( cvv wcel caltop wceq wa wb altopthbg mp2an ) AGHDGHABICDIJACJBDJKLEFABC
      DGGMN $.
      $( [12-May-2012] $) $( [14-Apr-2012] $)
  $}

  ${
    altopthc.1 $e |- B e. _V $.
    altopthc.2 $e |- C e. _V $.
    $( Alternate ordered pair theorem with different sethood requirements.  See
       ~ altopth for more comments.  (Contributed by Scott Fenton,
       14-Apr-2012.) $)
    altopthc $p |- ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) $=
      ( caltop wceq wa eqcom altopthb anbi12i 3bitri ) ABGZCDGZHONHCAHZDBHZIACH
      ZBDHZINOJCDABFEKPRQSCAJDBJLM $.
      $( [12-May-2012] $) $( [14-Apr-2012] $)
  $}

  ${
    altopthd.1 $e |- C e. _V $.
    altopthd.2 $e |- D e. _V $.
    $( Alternate ordered pair theorem with different sethood requirements.  See
       ~ altopth for more comments.  (Contributed by Scott Fenton,
       14-Apr-2012.) $)
    altopthd $p |- ( << A , B >> = << C , D >> <-> ( A = C /\ B = D ) ) $=
      ( caltop wceq wa eqcom altopth anbi12i 3bitri ) ABGZCDGZHONHCAHZDBHZIACHZ
      BDHZINOJCDABEFKPRQSCAJDBJLM $.
      $( [12-May-2012] $) $( [14-Apr-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    $( Equality for alternate cross products.  (Contributed by Scott Fenton,
       24-Mar-2012.) $)
    altxpeq1 $p |- ( A = B -> ( A XX. C ) = ( B XX. C ) ) $=
      ( vz vx vy wceq cv caltop wrex cab caltxp rexeq abbidv df-altxp 3eqtr4g )
      ABGZDHEHFHIGFCJZEAJZDKREBJZDKACLBCLQSTDREABMNEFDACOEFDBCOP $.
      $( [11-Apr-2012] $) $( [24-Mar-2012] $)

    $( Equality for alternate cross products.  (Contributed by Scott Fenton,
       24-Mar-2012.) $)
    altxpeq2 $p |- ( A = B -> ( C XX. A ) = ( C XX. B ) ) $=
      ( vz vx vy wceq cv caltop wrex cab caltxp rexbidv abbidv df-altxp 3eqtr4g
      rexeq ) ABGZDHEHFHIGZFAJZECJZDKSFBJZECJZDKCALCBLRUAUCDRTUBECSFABQMNEFDCAO
      EFDCBOP $.
      $( [11-Apr-2012] $) $( [24-Mar-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d X x y z $.
    $( Membership in alternate cross products.  (Contributed by Scott Fenton,
       23-Mar-2012.) $)
    elaltxp $p |- ( X e. ( A XX. B ) <->
                     E. x e. A E. y e. B X = << x , y >> ) $=
      ( vz caltxp wcel cvv cv caltop wceq wrex elex wi altopex eleq1 mpbiri a1i
      wa rexlimivv eqeq1 2rexbidv df-altxp elab2g pm5.21nii ) ECDGZHEIHZEAJZBJZ
      KZLZBDMACMZEUGNULUHABCDULUHOUICHUJDHTULUHUKIHUIUJPEUKIQRSUAFJZUKLZBDMACMU
      MFEUGIUNELUOULABCDUNEUKUBUCABFCDUDUEUF $.
      $( [11-Apr-2012] $) $( [23-Mar-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d X x y $.  $d Y x y $.
    $( Alternate ordered pair membership in a cross product.  Note that, unlike
       ~ opelxp , there is no sethood requirement here.  (Contributed by Scott
       Fenton, 22-Mar-2012.) $)
    altopelaltxp $p |- ( << X , Y >> e. ( A XX. B ) <->
                          ( X e. A /\ Y e. B ) ) $=
      ( vx vy caltop caltxp wcel cv wceq wrex wa elaltxp reeanv eqcom vex bitri
      altopth risset 2rexbii anbi12i 3bitr4i ) CDGZABHIUDEJZFJZGZKZFBLEALZCAIZD
      BIZMZEFABUDNUECKZUFDKZMZFBLEALUMEALZUNFBLZMUIULUMUNEFABOUHUOEFABUHUGUDKUO
      UDUGPUEUFCDEQFQSRUAUJUPUKUQECATFDBTUBUCR $.
      $( [11-Apr-2012] $) $( [22-Mar-2012] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( An inclusion rule for alternate cross products.  (Contributed by Scott
       Fenton, 24-Mar-2012.) $)
    altxpsspw $p |- ( A XX. B ) C_ ~P ~P ( A u. ~P B ) $=
      ( vz vx vy caltxp cpw cun cv wcel wrex wa csn cpr wss snssi syl snex elpw
      prex caltop wceq elaltxp df-altop ssun3 adantr elun1 elun2 sylbir anim12i
      wi prss sylib prsspw bitri sylanbrc syl5eqel eleq1a rexlimivv sylbi ssriv
      vex ) CABFZABGZHZGZGZCIZVCJVHDIZEIZUAZUBZEBKDAKVHVGJZDEABVHUCVLVMDEABVIAJ
      ZVJBJZLZVKVGJVLVMUKVPVKVIMZVIVJMZNZNZVGVIVJUDVPVQVEOZVSVEOZVTVGJZVNWAVOVN
      VQAOWAVIAPVQAVDUEQUFVPVIVEJZVRVEJZLWBVNWDVOWEVIAVDUGVOVRBOZWEVJBPWFVRVDJW
      EVRBVJRZSVRVDAUHUIQUJVIVRVEDVBWGULUMWCVTVFOWAWBLVTVFVQVSTSVQVSVEVIRVIVRTU
      NUOUPUQVKVGVHURQUSUTVA $.
      $( [12-May-2012] $) $( [24-Mar-2012] $)
  $}

  $( The alternate cross product of two sets is a set.  (Contributed by Scott
     Fenton, 24-Mar-2012.) $)
  altxpexg $p |- ( ( A e. V /\ B e. W ) -> ( A XX. B ) e. _V ) $=
    ( wcel wa caltxp cpw cun wss cvv altxpsspw pwexg unexg sylan2 ssexg sylancr
    3syl ) ACEZBDEZFZABGZABHZIZHZHZJUFKEZUBKEABLUAUDKEZUEKEUGTSUCKEUHBDMAUCCKNO
    UDKMUEKMRUBUFKPQ $.
    $( [11-Apr-2012] $) $( [24-Mar-2012] $)

  $( Compute the rank of an alternate ordered pair.  (Contributed by Scott
     Fenton, 18-Dec-2013.) $)
  rankaltopb $p |- ( ( A e. U. ( R1 " On ) /\ B e. U. ( R1 " On ) ) -> ( rank
    ` << A , B >> ) = suc suc ( ( rank ` A ) u. suc ( rank ` B ) ) ) $=
    ( cr1 con0 cima wcel wa crnk cfv csn cun csuc wceq snwf cpr rankprb syl2anc
    fveq2i suceq eqtrd cuni caltop df-altop adantr prwf syl5eq wss snsspr1 mpbi
    ssequn1 rankunb syl5eqr eqtr3d syl sylan2 ranksnb uneq2d 3syl adantl ) ACDE
    UAZFZBUTFZGABUBZHIZAHIZBJZHIZKZLZLZVEBHILZKZLZLZVBVAVFUTFZVDVJMBNVAVOGZVDAJ
    ZHIAVFOZHIZKZLZVJVPVDVQVROZHIZWAVCWBHABUCRVPVQUTFZVRUTFZWCWAMVAWDVOANUDZAVF
    UEZVQVRPQUFVPVTVIMWAVJMVPVSVTVIVPVSVQVRKZHIZVTWHVRHVQVRUGWHVRMAVFUHVQVRUJUI
    RVPWDWEWIVTMWFWGVQVRUKQULAVFPUMVTVISUNTUOVBVJVNMZVAVBVHVLMVIVMMWJVBVGVKVEBU
    PUQVHVLSVIVMSURUST $.
    $( [19-Apr-2014] $) $( [18-Dec-2013] $)

