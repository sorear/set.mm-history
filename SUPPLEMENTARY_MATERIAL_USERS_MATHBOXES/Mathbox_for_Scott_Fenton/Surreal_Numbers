$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Set up the new syntax for surreal numbers $)
  $c No <s bday $.
  $( Declare the class of all surreal numbers (see ~ df-no ). $)
  csur $a class No $.
  $( Declare the less than relationship over surreal numbers (see
     ~ df-slt ). $)
  cslt $a class <s $.
  $( Declare the birthday function for surreal numbers (see ~ df-bday ). $)
  cbday $a class bday $.

  ${
    $d f a $.
    $( Define the class of surreal numbers.  The surreal numbers are a proper
       class of numbers developed by John H. Conway and introduced by Donald
       Knuth in 1975.  They form a proper class into which all ordered fields
       can be embedded.  The approach we take to defining them was first
       introduced by Hary Goshnor, and is based on the conception of a "sign
       expansion" of a surreal number.  We define the surreals as
       ordinal-indexed sequences of ` 1o ` and ` 2o ` , analagous to Goshnor's
       ` ( - ) ` and ` ( + ) ` .

       After introducing this definition, we will abstract away from it using
       axioms that Norman Alling developed in "Foundations of Analysis over
       Surreal Number Fields."  This is done in a effort to be agnostic towards
       the exact implementation of surreals. $)
    df-no $a |- No = { f | E. a e. On f : a --> { 1o , 2o } } $.
  $}

  ${
    $d f g x y $.
    $( Next, we introduce surreal less-than, a comparison relationship over the
       surreals by lexicographically ordering them. $)
    df-slt $a |- <s = { <. f , g >. | ( ( f e. No /\ g e. No ) /\
                         E. x e. On (
                            A. y e. x ( f ` y ) = ( g ` y ) /\
                              ( f ` x ) { <. 1o , (/) >. ,
                                          <. 1o , 2o >. ,
                                          <. (/) , 2o >. } ( g ` x ) ) ) } $.
  $}

  $( Finally, we introduce the birthday function.  This function maps each
     surreal to an ordinal.  In our implementation, this is the domain of the
     sign function.  The important properties of this function are established
     later. $)
  df-bday $a |- bday = ( x e. No |-> dom x ) $.

  ${
    $d A f x $.
    $( Membership in the surreals.  (Shortened proof on 2012-Apr-14, SF).
       (Contributed by Scott Fenton, 11-Jun-2011.) $)
    elno $p |- ( A e. No <-> E. x e. On A : x --> { 1o , 2o } ) $=
      ( vf csur wcel cvv cv c1o c2o cpr wf con0 wrex elex ancoms rexlimiva wceq
      fex feq1 rexbidv df-no elab2g pm5.21nii ) BDEBFEZAGZHIJZBKZALMZBDNUGUDALU
      GUELEUDUEUFLBROPUEUFCGZKZALMUHCBDFUIBQUJUGALUEUFUIBSTCAUAUBUC $.
      $( [12-May-2012] $) $( [11-Jun-2011] $)
  $}

  ${
    $d A f g x y $.  $d B f g x y $.
    $( The value of the surreal less than relationship.  (Contributed by Scott
       Fenton, 14-Jun-2011.) $)
    sltval $p |- ( ( A e. No /\ B e. No ) ->
            ( A <s B <-> E. x e. On ( A. y e. x ( A ` y ) = ( B ` y ) /\
                         ( A ` x ) { <. 1o , (/) >. , <. 1o , 2o >. ,
                                     <. (/) , 2o >. } ( B ` x ) ) ) ) $=
      ( vf vg csur wcel wa cslt wbr cfv wceq wral cop con0 wrex fveq1 anbi12d
      cv c1o c0 c2o ctp eleq1 anbi1d eqeq1d ralbidv breq1d anbi2d eqeq2d breq2d
      rexbidv df-slt brabg bianabs ) CGHZDGHZIZCDJKBTZCLZUTDLZMZBATZNZVDCLZVDDL
      ZUAUBOUAUCOUBUCOUDZKZIZAPQZETZGHZFTZGHZIZUTVLLZUTVNLZMZBVDNZVDVLLZVDVNLZV
      HKZIZAPQZIUQVOIZVAVRMZBVDNZVFWBVHKZIZAPQZIUSVKIEFCDGGJVLCMZVPWFWEWKWLVMUQ
      VOVLCGUEUFWLWDWJAPWLVTWHWCWIWLVSWGBVDWLVQVAVRUTVLCRUGUHWLWAVFWBVHVDVLCRUI
      SUMSVNDMZWFUSWKVKWMVOURUQVNDGUEUJWMWJVJAPWMWHVEWIVIWMWGVCBVDWMVRVBVAUTVND
      RUKUHWMWBVGVFVHVDVNDRULSUMSABEFUNUOUP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( The value of the birthday function within the surreals.  (Contributed by
       Scott Fenton, 14-Jun-2011.) $)
    bdayval $p |- ( A e. No -> ( bday ` A ) = dom A ) $=
      ( vx csur wcel cdm cvv cbday cfv wceq dmexg cv dmeq df-bday fvmptg mpdan
      ) ACDAEZFDAGHPIACJBABKZEPCFGQALBMNO $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( A surreal is a function.  (Contributed by Scott Fenton, 16-Jun-2011.) $)
    nofun $p |- ( A e. No -> Fun A ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex wfun elno ffun rexlimivw sylbi
      ) ACDBEZFGHZAIZBJKALZBAMSTBJQRANOP $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The domain of a surreal is an ordinal.  (Contributed by Scott Fenton,
       16-Jun-2011.) $)
    nodmon $p |- ( A e. No -> dom A e. On ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex cdm elno fdm biimprcd rexlimiv
      eleq1d sylbi ) ACDBEZFGHZAIZBJKALZJDZBAMUAUCBJUAUCSJDUAUBSJSTANQOPR $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The range of a surreal is a subset of the surreal signs.  (Contributed
       by Scott Fenton, 16-Jun-2011.) $)
    norn $p |- ( A e. No -> ran A C_ { 1o , 2o } ) $=
      ( vx csur wcel cv c1o c2o cpr con0 wrex crn wss elno frn rexlimivw sylbi
      wf ) ACDBEZFGHZAQZBIJAKSLZBAMTUABIRSANOP $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  $( The domain of a surreal has the ordinal property.  (Contributed by Scott
     Fenton, 16-Jun-2011.) $)
  nodmord $p |- ( A e. No -> Ord dom A ) $=
    ( csur wcel cdm con0 word nodmon eloni syl ) ABCADZECJFAGJHI $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  ${
    $d x A $.
    $( An alternative condition for membership in ` No ` .  (Contributed by
       Scott Fenton, 21-Mar-2012.) $)
    elno2 $p |- ( A e. No <->
              ( Fun A /\ dom A e. On /\ ran A C_ { 1o , 2o } ) ) $=
      ( vx csur wcel wfun cdm con0 crn c1o c2o cpr wss w3a nofun nodmon norn wf
      3jca wa sylibr cv wrex simp2 wceq simpl eqidd df-fn sylanbrc anim1i 3impa
      wfn df-f feq2 rcla4ev syl2anc elno impbii ) ACDZAEZAFZGDZAHIJKZLZMZURUSVA
      VCANAOAPRVDBUAZVBAQZBGUBZURVDVAUTVBAQZVGUSVAVCUCVDAUTUKZVCSZVHUSVAVCVJUSV
      ASZVIVCVKUSUTUTUDVIUSVAUEVKUTUFAUTUGUHUIUJUTVBAULTVFVHBUTGVEUTVBAUMUNUOBA
      UPTUQ $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  $( Another condition for membership in ` No ` .  (Contributed by Scott
     Fenton, 14-Apr-2012.) $)
  elno3 $p |- ( A e. No <-> ( A : dom A --> { 1o , 2o } /\ dom A e. On ) ) $=
    ( wfun cdm con0 wcel crn c1o c2o cpr wss w3a wa csur wf 3ancomb bitri elno2
    df-3an wfn anbi1i df-f funfn bitr4i 3bitr4i ) ABZACZDEZAFGHIZJZKZUEUILZUGLZ
    AMEUFUHANZUGLUJUEUIUGKULUEUGUIOUEUIUGRPAQUMUKUGUMAUFSZUILUKUFUHAUAUEUNUIAUB
    TUCTUD $.
    $( [12-May-2012] $) $( [14-Apr-2012] $)

  ${
    $d A a x y $.  $d B a x y $.
    $( Alternate expression for surreal less than.  Two surreals obey surreal
       less than iff they obey the sign ordering at the first place they
       differ.  (Contributed by Scott Fenton, 17-Jun-2011.) $)
    sltval2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B <->
                       ( A ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                       { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                       ( B ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                     ) ) $=
      ( vy vx wcel wa cfv wceq c1o c0 c2o con0 wne wn df-ne mtbiri adantl fveq2
      fvex csur cslt wbr cv wral cop ctp wrex crab sltval cvv w3o 0ex 2on elexi
      cint brtp 1n0 mpbi eqeq1 fvprc nsyl2 adantr 2on0 3jaoi sylbi sylib onelon
      onintrab expcom syl5 neeq12d onnminsb com12 syld con2bii syl6ibr ralrimiv
      weq jca ex impac anass raleq breq12d anbi12d rcla4ev syl wi eqeq12 wb 1on
      csuc 0elon suc11 necon3bid mp2an mpbir df-2o df-1o eqeq12i nemtbir syl6bb
      eqcom mtbi 3imtr4i elrab biimpri adantlr ssrab2 ne0i onint sylancr hbrab1
      hbint ax-17 hbfv hbne elrabf simprbi eqeq12d rcla4cv ad2antlr mtod simpll
      oninton ontri1 syl2anc mpbird intss1 syldan sylan2 fveq2d pm2.43d expimpd
      wss eqssd biimpd rexlimiv impbid1 bitr4d ) AUAFBUAFGZABUBUCDUDZAHZUUCBHZI
      ZDEUDZUEZUUGAHZUUGBHZJKUFJLUFKLUFUGZUCZGZEMUHZCUDZAHZUUOBHZNZCMUIZUPZAHZU
      UTBHZUUKUCZEDABUJUUBUVCUUNUUBUVCUUNUUBUVCGZUUTMFZUUFDUUTUEZUVCGZGZUUNUVDU
      VEUVFGZUVCGUVHUUBUVCUVIUUBUVCUVIUVDUVEUVFUVCUVEUUBUVCUUTUKFZUVEUVCUVAJIZU
      VBKIZGZUVKUVBLIZGZUVAKIZUVNGZULUVJJKJLKLUVAUVBUUTATUUTBTUMLMUNUOZUVRUQUVM
      UVJUVOUVQUVKUVJUVLUVKUVPUVJUVKUVPJKIZJKNZUVSOURJKPUSZUVAJKUTQUUTAVAVBZVCU
      VKUVJUVNUWBVCUVNUVJUVPUVNUVLUVJUVNUVLLKIZLKNUWCOVDLKPUSZUVBLKUTQUUTBVAVBR
      VEVFUURCVIVGRZUVDUUFDUUTUVDUUCUUTFZUUDUUENZOZUUFUWFUVDUWHUWFUVDUUCMFZUWHU
      VDUVEUWFUWIUWEUVEUWFUWIUUTUUCVHVJVKUWIUWFUWHUURUWGCUUCCDVSUUPUUDUUQUUEUUO
      UUCASUUOUUCBSVLVMVNVOVNUWGUUFUUDUUEPVPVQVRVTWAWBUVEUVFUVCWCVGUUMUVGEUUTMU
      UGUUTIZUUHUVFUULUVCUUFDUUGUUTWDUWJUUIUVAUUJUVBUUKUUGUUTASUUGUUTBSWEWFWGWH
      WAUUMUVCEMUUGMFZUUHUULUVCUWKUUHGZUULUVCUWLUULUULUVCWIUWLUULGZUULUVCUWMUUI
      UVAUUJUVBUUKUWMUUGUUTAUULUWLUUIUUJNZUWJUUIJIZUUJKIGZUWOUUJLIZGZUUIKIUWQGZ
      ULUUIUUJIZOZUULUWNUWPUXAUWRUWSUWPUWTUVSUWAUUIJUUJKWJQUWRUWTLJIZUXBJWMZKWM
      ZUXCUXDNZUVTURJMFZKMFZUXEUVTWKWLWNUXFUXGGUXCUXDJKJKWOWPWQWRLUXCJUXDWSWTXA
      XBUWRUWTJLIUXBUUIJUUJLWJJLXDXCQUWSUWTKLIZUWCUXHUWDLKXDXEUUIKUUJLWJQVEJKJL
      KLUUIUUJUUGATUUGBTUMUVRUVRUQUUIUUJPXFUWLUWNUUGUUSFZUWJUWKUWNUXIUUHUXIUWKU
      WNGUURUWNCUUGMCEVSUUPUUIUUQUUJUUOUUGASUUOUUGBSVLXGXHXIUWLUXIGZUUGUUTUXJUU
      GUUTYPZUUTUUGFZOZUXJUXLUVAUVBIZUXJUVAUVBNZUXNOUXJUUTUUSFZUXOUXJUUSMYPZUUS
      KNZUXPUURCMXJZUXIUXRUWLUUSUUGXKZRUUSXLXMUXPUVEUXOUURUXOCEUUTMCEUUSUURCEMX
      NXOZUWKCXPCEUVAUVBCEUUTAUUGAFCXPUYAXQCEUUTBUUGBFCXPUYAXQXRUUOUUTIUUPUVAUU
      QUVBUUOUUTASUUOUUTBSVLXSXTWHUVAUVBPVGUUHUXLUXNWIUWKUXIUUFUXNDUUTUUGUUCUUT
      IUUDUVAUUEUVBUUCUUTASUUCUUTBSYAYBYCYDUXJUWKUVEUXKUXMWKUWKUUHUXIYEUXIUVEUW
      LUXIUXQUXRUVEUXSUXTUUSYFXMRUUGUUTYGYHYIUXIUUTUUGYPUWLUUGUUSYJRYQYKYLZYMUW
      MUUGUUTBUYBYMWEYRWAYNYOYSYTUUA $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $( The function value of a surreal is either a sign or the empty set.
       (Contributed by Scott Fenton, 22-Jun-2011.) $)
    nofv $p |- ( A e. No -> ( ( A ` X ) = (/) \/ ( A ` X ) = 1o \/
                               ( A ` X ) = 2o ) ) $=
      ( csur wcel cfv c0 wceq c1o c2o wo w3o cdm wn pm2.1 wi ndmfv a1i wa elexi
      con0 wfun crn cpr wss nofun norn fvelrn syl5com impancom 1on elpr2 syl6ib
      ssel 2on syl2anc orim12d mpi 3orass sylibr ) ACDZBAEZFGZVAHGZVAIGZJZJZVBV
      CVDKUTBALDZMZVGJVFVGNUTVHVBVGVEVHVBOUTBAPQUTAUAZAUBZHIUCZUDZVGVEOAUEAUFVI
      VLRVGVAVKDZVEVIVGVLVMVIVGRVAVJDVLVMBAUGVJVKVAUMUHUIVAHIHTUJSITUNSUKULUOUP
      UQVBVCVDURUS $.
      $( [10-Jul-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    $( ` (/) ` is not a surreal sign.  (Contributed by Scott Fenton,
       16-Jun-2011.) $)
    nosgnn0 $p |- -. (/) e. { 1o , 2o } $=
      ( c0 c1o c2o cpr wcel wceq wo wne 1n0 necom df-ne bitri mpbi csuc nsuceq0
      wn df-2o neeq2i bitr4i pm3.2ni 0ex elpr mtbir ) ABCDEABFZACFZGUDUEBAHZUDP
      ZIUFABHUGBAJABKLMACHZUEPBNZAHZUHBOUJAUIHUHUIAJCUIAQRSMACKMTABCUAUBUC $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    nosgnn0i.1 $e |- X e. { 1o , 2o } $.
    $( If ` X ` is a surreal sign, then it is not null.  (Contributed by Scott
       Fenton, 3-Aug-2011.) $)
    nosgnn0i $p |- (/) =/= X $=
      ( c0 wne wceq wn c1o c2o cpr wcel nosgnn0 eleq1 mpbiri mto df-ne mpbir )
      CADCAEZFQCGHIZJZKQSARJBCARLMNCAOP $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The restriction of a surreal to an ordinal is still a surreal.
       (Contributed by Scott Fenton, 4-Sep-2011.) $)
    noreson $p |- ( ( A e. No /\ B e. On ) -> ( A |` B ) e. No ) $=
      ( vx vy cv c1o c2o cpr wf con0 wrex wcel wa cres csur wi onin fresin elno
      cin feq2 rcla4ev syl2an an32s ex rexlimiva imp anbi1i 3imtr4i ) CEZFGHZAI
      ZCJKZBJLZMDEZUKABNZIZDJKZAOLZUNMUPOLUMUNURULUNURPCJUJJLZULMUNURUTUNULURUT
      UNMUJBTZJLVAUKUPIZURULUJBQUJUKABRUQVBDVAJUOVAUKUPUAUBUCUDUEUFUGUSUMUNCASU
      HDUPSUI $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A k $.  $d B k $.

    $( If ` A <s B ` , then the sign of ` A ` at the first place they differ is
       either undefined or ` 1o ` (Contributed by Scott Fenton, 4-Sep-2011.) $)
    sltsgn1 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 1o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab c1o cop c2o wceq fvex adantr
      c0 cint ctp wo sltval2 w3o 0ex 2on elexi brtp olc orc 3jaoi sylbi syl6bi
      ) ADEBDEFABGHCIZAJUOBJKCLMUAZAJZUPBJZNTONPOTPOUBHZUQTQZUQNQZUCZABCUDUSVAU
      RTQZFZVAURPQZFZUTVEFZUEVBNTNPTPUQURUPARUPBRUFPLUGUHZVHUIVDVBVFVGVAVBVCVAU
      TUJZSVAVBVEVISUTVBVEUTVAUKSULUMUN $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)

    $( If ` A <s B ` , then the sign of ` B ` at the first place they differ is
       either undefined or ` 2o ` (Contributed by Scott Fenton, 4-Sep-2011.) $)
    sltsgn2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 2o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab c1o cop c2o wceq fvex adantl
      c0 cint ctp wo sltval2 w3o 0ex 2on elexi brtp orc olc 3jaoi sylbi syl6bi
      ) ADEBDEFABGHCIZAJUOBJKCLMUAZAJZUPBJZNTONPOTPOUBHZURTQZURPQZUCZABCUDUSUQN
      QZUTFZVCVAFZUQTQZVAFZUEVBNTNPTPUQURUPARUPBRUFPLUGUHZVHUIVDVBVEVGUTVBVCUTV
      AUJSVAVBVCVAUTUKZSVAVBVFVISULUMUN $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( If ` A <s B ` , then the intersection of all the ordinals that have
       differing signs in ` A ` and ` B ` exists.  (Contributed by Scott
       Fenton, 22-Feb-2012.) $)
    sltintdifex $p |- ( ( A e. No /\ B e. No ) ->
       ( A <s B -> |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. _V ) ) $=
      ( csur wcel wa wbr cfv wne con0 c1o c0 cop c2o wceq fvex fvprc df-ne mpbi
      wn cslt crab cint ctp cvv sltval2 w3o 0ex 2on elexi brtp 1n0 eqeq1 syl6bb
      cv eqcom mtbiri syl con4i adantr 2on0 adantl 3jaoi sylbi syl6bi ) ADEBDEF
      ABUAGCUOZAHVFBHICJUBUCZAHZVGBHZKLMKNMLNMUDGZVGUEEZABCUFVJVHKOZVILOZFZVLVI
      NOZFZVHLOZVOFZUGVKKLKNLNVHVIVGAPVGBPUHNJUIUJZVSUKVNVKVPVRVLVKVMVKVLVKTZVQ
      VLTVGAQVQVLKLOZKLIWATULKLRSVQVLLKOWAVHLKUMLKUPUNUQURUSZUTVLVKVOWBUTVOVKVQ
      VKVOVTVMVOTVGBQVMVONLOZNLIWCTVANLRSVMVOLNOWCVILNUMLNUPUNUQURUSVBVCVDVE $.
      $( [22-Mar-2012] $) $( [22-Feb-2012] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.  $d X a x y $.
    $( Lemma for axfe (future) .  If the restrictions of two surreals to a
       given ordinal obey surreal less than, then so do the two surreals
       themselves. $)
    sltres $p |- ( ( A e. No /\ B e. No /\ X e. On ) ->
                      ( ( A |` X ) <s ( B |` X ) -> A <s B ) ) $=
      ( vy va wcel con0 wbr wa cfv wceq c1o c0 c2o wi imp wn syl sylc ex vx w3a
      csur cres cslt cv wral cop ctp wrex wne crab cint noreson 3adant2 3adant1
      cvv sltintdifex onintrab syl6ib syl2anc wss simpl3 cdm wo wb sltval2 fvex
      w3o 0ex 2on elexi brtp 1n0 df-ne mpbi eqeq1 mtbiri ndmfv nsyl2 orcd prid2
      adantr nosgnn0i eqcom syl6bb adantl 3jaoi sylbi syl6bi dmres eleq2i bitri
      olcd cin elin simplbi jaoi onelss sselda onelon sylan intss1 ontri1 con2d
      syl5ib impancom mpd weq fveq2 neeq12d elrab simplbi2 con3d con2bii sylibr
      fvres eqeq12d biimpd ralrimiva fvresval ori eqcomd eqeq2 a1i ad2antrl cpr
      mpbid wfun nofun fvelrn norn sseld syld nosgnn0 eleq1 nsyli adantrl jcad
      crn anim12i ad2antll adantrr syl6 3orim123d 3imtr4g raleq breq12d anbi12d
      sylbid rcla4ev syl12anc sltval 3adant3 mpbird ) AUCFZBUCFZCGFZUBZACUDZBCU
      DZUEHZABUEHZUUSUVBIZUVCDUFZAJZUVEBJZKZDUAUFZUGZUVIAJZUVIBJZLMUHLNUHMNUHUI
      ZHZIZUAGUJZUVDEUFZUUTJZUVQUVAJZUKZEGULZUMZGFZUVHDUWBUGZUWBAJZUWBBJZUVMHZU
      VPUUSUVBUWCUUSUUTUCFZUVAUCFZUVBUWCOUUPUURUWHUUQACUNUOZUUQUURUWIUUPBCUNUPZ
      UWHUWIIUVBUWBUQFUWCUUTUVAEURUVTEUSUTVAPZUVDUVHDUWBUVDUVEUWBFZIZUVECFZUVEU
      UTJZUVEUVAJZKZUVHUVDUWBCUVEUVDUURUWBCFZUWBCVBUUPUUQUURUVBVCUVDUWBUUTVDZFZ
      UWBUVAVDZFZVEZUWSUUSUVBUXDUUSUVBUWBUUTJZUWBUVAJZUVMHZUXDUUSUWHUWIUVBUXGVF
      UWJUWKUUTUVAEVGVAZUXGUXELKZUXFMKZIZUXIUXFNKZIZUXEMKZUXLIZVIZUXDLMLNMNUXEU
      XFUWBUUTVHUWBUVAVHVJNGVKVLZUXQVMZUXKUXDUXMUXOUXKUXAUXCUXIUXAUXJUXIUXNUXAU
      XIUXNLMKZLMUKUXSQVNLMVOVPUXELMVQVRZUWBUUTVSVTZWCWAUXMUXAUXCUXIUXAUXLUYAWC
      WAUXOUXCUXAUXLUXCUXNUXLUXJUXCUXLUXJMNKZMNUKUYBQNLNUXQWBWDMNVOVPUXLUXJNMKU
      YBUXFNMVQNMWEWFVRZUWBUVAVSVTZWGWNWHWIWJPUXAUWSUXCUXAUWSUWBAVDZFZUXAUWBCUY
      EWOZFUWSUYFIUWTUYGUWBACWKWLUWBCUYEWPWMZWQZUXCUWSUWBBVDZFZUXCUWBCUYJWOZFUW
      SUYKIUXBUYLUWBBCWKWLUWBCUYJWPWMZWQZWRRCUWBWSSWTUWNUWPUWQUKZQZUWRUWNUVEGFZ
      UVEUWAFZQZUYPUVDUWCUWMUYQUWLUWBUVEXAXBZUWNUYQUYSUYTUVDUYQUWMUYSUVDUWCUYQU
      WMUYSOUWLUWCUYQIZUYRUWMUYRUWBUVEVBVUAUWMQUVEUWAXCUWBUVEXDXFXEXBXGXHUYQUYO
      UYRUYRUYQUYOUVTUYOEUVEGEDXIUVRUWPUVSUWQUVQUVEUUTXJUVQUVEUVAXJXKXLXMXNSUYO
      UWRUWPUWQVOXOXPUWOUWRUVHUWOUWPUVFUWQUVGUVECAXQUVECBXQXRXSSXTUUSUVBUWGUUSU
      VBUXGUWGUXHUUSUXPUWELKZUWFMKZIZVUBUWFNKZIZUWEMKZVUEIZVIUXGUWGUUSUXKVUDUXM
      VUFUXOVUHUUSUXKVUBVUCUXKVUBOUUSUXIVUBUXJUXIUWEUXEKVUBUXIUXEUWEUXIUXNUXEUW
      EKZUXTVUIUXNUWBCAYAYBVTYCUXELUWEYDYHZWCYEUUSUXKVUCUUSUXKIZUYKQZVUCVUKUWSU
      XCQZVULVUKUXAUWSUXIUXAUUSUXJUYAYFUYIRUUSUXJVUMUXIUUSUXJVUMUUSUWIUXJVUMOUW
      KUWIUXCUXFLNYGZFZUXJUWIUXCUXFUVAYTZFZVUOUWIUVAYIZUXCVUQOUVAYJVURUXCVUQUWB
      UVAYKTRUWIVUPVUNUXFUVAYLYMYNUXJVUOMVUNFZYOUXFMVUNYPVRYQRPYRUWSUYKUXCUXCUW
      SUYKUYMXMXNSUWBBVSRTYSUXMVUFOUUSUXIVUBUXLVUEVUJUXLUWFUXFKVUEUXLUXFUWFUXLU
      XJUXFUWFKZUYCVUTUXJUWBCBYAYBVTYCUXFNUWFYDYHZUUAYEUUSUXOVUGVUEUUSUXOUYFQZV
      UGUUSUXOVVBUUSUXOIZUWSUXAQZVVBVVCUXCUWSUXLUXCUUSUXNUYDUUBUYNRUUSUXNVVDUXL
      UUSUXNVVDUUSUWHUXNVVDOUWJUWHUXAUXEVUNFZUXNUWHUXAUXEUUTYTZFZVVEUWHUUTYIZUX
      AVVGOUUTYJVVHUXAVVGUWBUUTYKTRUWHVVFVUNUXEUUTYLYMYNUXNVVEVUSYOUXEMVUNYPVRY
      QRPUUCUWSUYFUXAUXAUWSUYFUYHXMXNSTUWBAVSUUDUXOVUEOUUSUXLVUEUXNVVAWGYEYSUUE
      UXRLMLNMNUWEUWFUWBAVHUWBBVHVJUXQUXQVMUUFUUJPUVOUWDUWGIUAUWBGUVIUWBKZUVJUW
      DUVNUWGUVHDUVIUWBUUGVVIUVKUWEUVLUWFUVMUVIUWBAXJUVIUWBBXJUUHUUIUUKUULUUSUV
      CUVPVFZUVBUUPUUQVVJUURUADABUUMUUNWCUUOT $.
      $( [9-Mar-2012] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A x $.  $d X x $.
    noxpsgn.1 $e |- X e. { 1o , 2o } $.
    $( The cross product of an ordinal and the singleton of a sign is a
       surreal.  (Contributed by Scott Fenton, 21-Jun-2011.) $)
    noxpsgn $p |- ( A e. On -> ( A X. { X } ) e. No ) $=
      ( vx con0 wcel cv c1o c2o cpr csn cxp wf wrex csur elexi fconst snss mpbi
      wss fss mp2an feq2 rcla4ev mpan2 elno sylibr ) AEFZDGZHIJZABKZLZMZDENZULO
      FUHAUJULMZUNAUKULMUKUJTZUOABBUJCPZQBUJFUPCBUJUQRSAUKUJULUAUBUMUODAEUIAUJU
      LUCUDUEDULUFUG $.
      $( [10-Jul-2011] $) $( [21-Jun-2011] $)
  $}

  $( The cross product of an ordinal and ` { 1o } ` is a surreal.  (Contributed
     by Scott Fenton, 12-Jun-2011.) $)
  noxp1o $p |- ( A e. On -> ( A X. { 1o } ) e. No ) $=
    ( c1o c2o con0 1on elexi prid1 noxpsgn ) ABBCBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

  $( The cross product of an ordinal and ` { 2o } ` is a surreal.  (Contributed
     by Scott Fenton, 12-Jun-2011.) $)
  noxp2o $p |- ( A e. On -> ( A X. { 2o } ) e. No ) $=
    ( c2o c1o con0 2on elexi prid2 noxpsgn ) ABCBBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

