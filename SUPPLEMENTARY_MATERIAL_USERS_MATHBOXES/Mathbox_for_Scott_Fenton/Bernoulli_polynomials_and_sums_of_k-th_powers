$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Bernoulli polynomials and sums of k-th powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c BernPoly $.

  $( Declare the constant for the Bernoulli polynomial operator. $)
  cbp $a class BernPoly $.

  ${
    $d e f g k m n s x $.
    $( Define the Bernoulli polynomials.  Here we use well-founded recursion to
       define the Bernoulli polynomials.  This agrees with most textbook
       definitions, although explicit formulae do exist. $)
    df-bpoly $a |- BernPoly =
       ( m e. NN0 , x e. CC |->
         ( U. { f | E. s ( f Fn s /\
                           ( s C_ NN0 /\
                             A. e e. s Pred ( < , NN0 , e ) C_ s ) /\
                             A. e e. s ( f ` e ) =
          ( ( g e. _V |-> [_ ( # ` dom g ) / n ]_
            ( ( x ^ n ) - sum_ k e. dom g ( ( n _C k ) x.
              ( ( g ` k ) / ( ( n - k )
              + 1 ) ) ) ) ) ` ( f |` Pred ( < , NN0 , e ) ) ) ) } ` m ) ) $.
  $}

  ${
    $d e f g k m n s x F $.  $d e g k m n x N $.  $d a b c e f g k m n s x X $.
    $d e f s G $.
    ${
      bpoly.1 $e |- G = ( g e. _V |-> [_ ( # ` dom g ) / n ]_
         ( ( X ^ n ) - sum_ k e. dom g ( ( n _C k ) x.
           ( ( g ` k ) / ( ( n - k ) + 1 ) ) ) ) ) $.
      bpoly.2 $e |- F = U. { f | E. s ( f Fn s /\ ( s C_ NN0 /\
        A. e e. s Pred ( < , NN0 , e ) C_ s ) /\ A. e e. s ( f ` e ) =
          ( G ` ( f |` Pred ( < , NN0 , e ) ) ) ) } $.
      $( Lemma for ~ bpolyval . $)
      bpolylem $p |- ( ( N e. NN0 /\ X e. CC ) ->
         ( N BernPoly X ) = ( ( X ^ N ) -
            sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
              ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) $=
        ( cn0 wcel co cfv clt cmin cvv wceq vm vx cc wa cbp cpred cres cexp cc0
        c1 cfz cv cbc caddc cdiv cmul csu wfn wss wral cdm csb cmpt w3a wex cab
        chash cuni oveq1 oveq1d csbeq2dv mpteq2dv syl6eqr fveq1d eqeq2d ralbidv
        mpan2 3anbi3d exbidv abbidv unieqd fveq2 sylan9eqr df-bpoly ovmpt2a wwe
        fvex cuz ltweuz wb nn0uz weeq2 ax-mp mpbir nn0ex predasetex rgenw wfr2c
        eqid adantr prednn0 reseq2d fveq2d wfun wfr1 fnfun ovex resfunexg mp2an
        dmeq elfznn0 ssriv fnssres fndm syl6eq fveq1 sylan9eq oveq2d sumeq12rdv
        fvres csbeq1d eqtrd csbex fvmpt oveq2 oveq12d sumeq2sdv csbiegf cen wbr
        a17d cz nn0z fz01en syl cfn fzfi hashen sylibr hashfz1 syl2anr sumeq2dv
        simpr 3eqtr4d syl5eq 3eqtrd ) HMNZIUCNZUDZHIUEOHFPZFMQHUFZUGZGPZIHUHOZU
        IHUJROZUKOZHDULZUMOZUUQIUEOZHUUQROZUJUNOZUOOZUPOZDUQZROZUAUBHIMUCUAULZB
        ULZJULZURZUVHMUSMQAULZUFZUVHUSAUVHUTUDZUVJUVGPZUVGUVKUGZCSECULZVAZVGPZU
        BULZEULZUHOZUVPUVSUUQUMOZUUQUVOPZUVSUUQROZUJUNOZUOOZUPOZDUQZROZVBZVCZPZ
        TZAUVHUTZVDZJVEZBVFZVHZPZUUJUEUVRITZUVFHTUWRUVFFPZUUJUWSUVFUWQFUWSUWQUV
        IUVLUVMUVNGPZTZAUVHUTZVDZJVEZBVFZVHFUWSUWPUXFUWSUWOUXEBUWSUWNUXDJUWSUWM
        UXCUVIUVLUWSUWLUXBAUVHUWSUWKUXAUVMUWSUVNUWJGUWSUWJCSEUVQIUVSUHOZUWGROZV
        BZVCGUWSCSUWIUXIUWSUVQSNZUWIUXITUVPVGWGZUWSEUVQUWHUXHSUWSUVTUXGUWGRUVRI
        UVSUHVIVJVKVQVLKVMVNVOVPVRVSVTWALVMVNZUVFHFWBWCUBABCDUAEJWDZHFWGWEUUGUU
        JUUMTUUHJAMUXFQBFGHMQWFZUIWHPZQWFZUIWIMUXOTUXNUXPWJWKMUXOQWLWMWNZMQUVHU
        FSNJMMQUVHWOWPWQZUXFWSZLWRWTUUIUUMFUUPUGZGPZUVEUUIUULUXTGUUIUUKUUPFUUGU
        UKUUPTUUHHXAWTXBXCUUIUYAEUUPVGPZUXGUUPUWAUUQFPZUWDUOOZUPOZDUQZROZVBZUVE
        UXTSNZUYAUYHTFXDZUUPSNUYIFMURZUYJJAMUXFQBFGUXQUXRUXSLXEZMFXFWMUIUUOUKXG
        FUUPSXHXICUXTUXIUYHSGUVOUXTTZUXIEUVQUYGVBZUYHUYMUXJUXIUYNTUXKUYMEUVQUXH
        UYGSUYMUWGUYFUXGRUYMUVPUUPUWFUYEDUYMUVPUXTVAZUUPUVOUXTXJUXTUUPURZUYOUUP
        TUYKUUPMUSUYPUYLDUUPMUUQUUOXKZXLMUUPFXMXIUUPUXTXNWMXOZUYMUUQUUPNZUDZUWE
        UYDUWAUPUYTUWBUYCUWDUOUYMUYSUWBUUQUXTPUYCUUQUVOUXTXPUUQUUPFXTXQVJXRXSXR
        VKVQUYMEUVQUYBUYGUYMUVPUUPVGUYRXCYAYBKEUYBUYGUUPVGWGUXGUYFRXGYCYDWMUUIE
        HUYGVBZUUNUUPUURUYCUVAUOOZUPOZDUQZROZUYHUVEUUGVUAVUETUUHEAHUYGVUEMUUGUV
        JVUENEYKUVSHTZUXGUUNUYFVUDRUVSHIUHYEVUFUUPUYEVUCDVUFUWAUURUYDVUBUPUVSHU
        UQUMVIVUFUWDUVAUYCUOVUFUWCUUTUJUNUVSHUUQRVIVJXRYFYGYFYHWTUUIEUYBHUYGUUG
        UYBHTUUHUUGUYBUJHUKOZVGPZHUUGUUPVUGYIYJZUYBVUHTZUUGHYLNVUIHYMHYNYOUUPYP
        NVUGYPNVUJVUIWJUIUUOYQUJHYQUUPVUGYRXIYSHYTYBWTYAUUIUVDVUDUUNRUUIUUPUVCV
        UCDUUIUYSUDZUVBVUBUURUPVUKUUSUYCUVAUOUYSUUQMNUUHUUSUYCTUUIUYQUUGUUHUUCU
        AUBUUQIMUCUWRUYCUEUWSUVFUUQTUWRUWTUYCUXLUVFUUQFWBWCUXMUUQFWGWEUUAVJXRUU
        BXRUUDUUEYBUUF $.
        $( [23-Aug-2014] $) $( [22-May-2014] $)
    $}

    $( The value of the Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.) $)
    bpolyval $p |- ( ( N e. NN0 /\ X e. CC ) ->
       ( N BernPoly X ) = ( ( X ^ N ) -
          sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
            ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) $=
      ( ve vn vb vm vs cv cn0 wss wral cfv co cbc cmin c1 cdiv cmul wceq vf wfn
      vg va vx vc clt cpred cres cvv cdm chash cexp caddc csu cmpt w3a wex cuni
      wa cab fvex wcel ax-17 oveq2 oveq1 oveq1d oveq2d oveq12d sumeq2sdv csbief
      csb fveq2 cbvsumv dmeq fveq1 adantr sumeq12dv syl5eq mpan2 syl5eqr fveq2d
      csbeq1d eqtrd cbvmptv fneq2 sseq1 predeq3 sseq1d cbvralv sseq2 raleqbi1dv
      csbeq2dv syl5bb anbi12d raleq cbvexv fneq1 reseq2d eqeq12d reseq1 ralbidv
      3anbi123d 3anbi13d exbidv cbvabv unieqi bpolylem ) DUAUCAEUDIZUEIZUBZXJJK
      ZJUGFIZUHZXJKZFXJLZUTZXMXIMZXIXNUIZUFUJCUFIZUKZULMZUMNZYAYBGIZONZYDXTMZYB
      YDPNZQUNNZRNZSNZGUOZPNZUPZMZTZFXJLZUQZUEURZUDVAZUSYMBCHUFUCUJYLEUCIZUKZUL
      MZCEIZUMNZUUAUUCAIZONZUUEYTMZUUCUUEPNZQUNNZRNZSNZAUOZPNZVLZXTYTTZYLEYBUUM
      VLZUUNUUOYLEYBUUDYAUUCYDONZYFUUCYDPNZQUNNZRNZSNZGUOZPNZVLZUUPEDYBUVCYLYAU
      LVBZDIZYLVCEVDUUCYBTZUUDYCUVBYKPUUCYBCUMVEUVGYAUVAYJGUVGUUQYEUUTYISUUCYBY
      DOVFUVGUUSYHYFRUVGUURYGQUNUUCYBYDPVFVGVHVIVJVIVKUUOYBUJVCUVDUUPTUVEUUOEYB
      UVCUUMUJUUOUVBUULUUDPUUOUVBYAUUFUUEXTMZUUIRNZSNZAUOUULYAUVAUVJGAYDUUETZUU
      QUUFUUTUVISYDUUEUUCOVEUVKYFUVHUUSUUIRYDUUEXTVMUVKUURUUHQUNYDUUEUUCPVEVGVI
      VIVNUUOYAUUAUVJUUKAXTYTVOZUUOUVJUUKTUUEYAVCUUOUVIUUJUUFSUUOUVHUUGUUIRUUEX
      TYTVPVGVHVQVRVSVHWMVTWAUUOEYBUUBUUMUUOYAUUAULUVLWBWCWDWEYSUAIZHIZUBZUVNJK
      ZJUGUVFUHZUVNKZDUVNLZUTZUVFUVMMZUVMUVQUIZYMMZTZDUVNLZUQZHURZUAVAYRUWGUDUA
      YRXIUVNUBZUVTYOFUVNLZUQZHURXIUVMTZUWGYQUWJUEHXJUVNTZXKUWHXQUVTYPUWIXJUVNX
      IWFUWLXLUVPXPUVSXJUVNJWGXPUVQXJKZDXJLUWLUVSXOUWMFDXJXMUVFTZXNUVQXJJUGXMUV
      FWHZWIWJUWMUVRDXJUVNXJUVNUVQWKWLWNWOYOFXJUVNWPXCWQUWKUWJUWFHUWKUWHUVOUWIU
      WEUVTUVNXIUVMWRUWIUVFXIMZXIUVQUIZYMMZTZDUVNLUWKUWEYOUWSFDUVNUWNXRUWPYNUWR
      XMUVFXIVMUWNXSUWQYMUWNXNUVQXIUWOWSWBWTWJUWKUWSUWDDUVNUWKUWPUWAUWRUWCUVFXI
      UVMVPUWKUWQUWBYMXIUVMUVQXAWBWTXBWNXDXEWNXFXGXH $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at zero.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpoly0 $p |- ( X e. CC -> ( 0 BernPoly X ) = 1 ) $=
      ( vk cc wcel cc0 cbp co cexp c1 cmin cfz cv cbc wceq c0 ax-mp cz 0z eqtri
      csu caddc cdiv cmul cn0 0nn0 bpolyval mpan exp0 oveq1d clt wbr cr ltm1 wb
      0re peano2zm mp2an mpbi sumeq1i sum0 oveq2i ax-1cn subid1i syl6eq eqtrd
      fzn ) ACDZEAFGZAEHGZEEIJGZKGZEBLZMGVLAFGEVLJGIUAGUBGUCGZBTZJGZIEUDDVGVHVO
      NUEBEAUFUGVGVOIVNJGZIVGVIIVNJAUHUIVPIEJGIVNEIJVNOVMBTEVKOVMBVJEUJUKZVKONZ
      EULDVQUOEUMPEQDZVJQDZVQVRUNRVSVTREUPPEVJVFUQURUSVMBUTSVAIVBVCSVDVE $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at one.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpoly1 $p |- ( X e. CC -> ( 1 BernPoly X ) = ( X - ( 1 / 2 ) ) ) $=
      ( vk cc wcel c1 cbp co cc0 cmin cfz cbc caddc cdiv cmul csu ax-1cn syl6eq
      c2 wceq oveq12d cexp cv cn0 1nn0 bpolyval mpan exp1 subidi oveq2i sumeq1i
      cz 0z bpoly0 oveq1d oveq2d halfcl ax-mp mulid2i syl6eqel oveq2 bcn0 oveq1
      subid1i df-2 syl6eqr fsum1 sylancr eqtrd syl5eq ) ACDZEAFGZAEUAGZHEEIGZJG
      ZEBUBZKGZVOAFGZEVOIGZELGZMGZNGZBOZIGZAERMGZIGEUCDZVJVKWCSUDBEAUEUFVJVLAWB
      WDIAUGVJWBHHJGZWABOZWDVNWFWABVMHHJEPUHUIUJVJWGEHAFGZRMGZNGZWDVJHUKDWJCDWG
      WJSULVJWJWDCVJWJEWDNGWDVJWIWDENVJWHERMAUMUNUOWDECDWDCDPEUPUQZURQZWKUSWAWJ
      BHVOHSZVPEVTWINWMVPEHKGZEVOHEKUTWEWNESUDEVAUQQWMVQWHVSRMVOHAFVBWMVSEELGRW
      MVREELWMVREHIGEVOHEIUTEPVCQUNVDVETTVFVGWLVHVITVH $.
      $( [16-May-2014] $)
  $}

  ${
    $d N n $.  $d X k m n $.
    $( Closure law for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.)  (The proof was shortened by Mario Carneiro,
       22-May-2014.) $)
    bpolycl $p |- ( ( N e. NN0 /\ X e. CC ) -> ( N BernPoly X ) e. CC ) $=
      ( vk vm cn0 wcel cc cbp co cv wi wceq oveq1 eleq1d cc0 c1 cfz syl2anc syl
      cmin vn imbi2d wral r19.21v w3a cexp cbc caddc cdiv cmul bpolyval 3adant3
      csu simp2 simp1 expcl fzfid wa cz elfzelz bccl syl2an nn0cn wne 3ad2antl3
      rcla4cva cn cvv wss ovex fzssp1 ax-mp ax-1cn npcan oveq2d syl5sseq sselda
      sylancl fznn0sub sylan syldan nn0p1nn nncn nnne0 divcl mulcl fsumcl subcl
      syl3anc eqeltrd 3exp a2d syl5bi nn0sinds imp ) AEFBGFZABHIZGFZWPUAJZBHIZG
      FZKZWPCJZBHIZGFZKZWPWRKUACAWSXCLZXAXEWPXGWTXDGWSXCBHMNUBWSALZXAWRWPXHWTWQ
      GWSABHMNUBXFCOWSPTIZQIZUCWPXECXJUCZKWSEFZXBWPXECXJUDXLWPXKXAXLWPXKXAXLWPX
      KUEZWTBWSUFIZXJWSDJZUGIZXOBHIZWSXOTIZPUHIZUIIZUJIZDUMZTIZGXLWPWTYCLXKDWSB
      UKULXMXNGFZYBGFYCGFXMWPXLYDXLWPXKUNXLWPXKUOZBWSUPRXMXJYADXMOXIUQXMXOXJFZU
      RZXPGFZXTGFZYAGFYGXPEFZYHXMXLXOUSFYJYFYEXOOXIUTXOWSVAVBXPVCSYGXQGFZXSGFZX
      SOVDZYIXKXLYFYKWPXEYKCXOXJXCXOLXDXQGXCXOBHMNVFVEYGXSVGFZYLYGXREFZYNXMYFXO
      OWSQIZFZYOXMXJYPXOXMOXIPUHIZQIZXJYPXIVHFXJYSVIWSPTVJOXIVHVKVLXMYRWSOQXMWS
      GFZPGFYRWSLXMXLYTYEWSVCSVMWSPVNVRVOVPVQXMXLYQYOYEEXOOWSVSVTWAXRWBSZXSWCSY
      GYNYMUUAXSWDSXQXSWEWIXPXTWFRWGXNYBWHRWJWKWLWMWNWO $.
      $( [22-May-2014] $) $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.
    $( A sum for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.)  (The proof was shortened by Mario Carneiro,
       22-May-2014.) $)
    bpolysum $p |- ( ( N e. NN0 /\ X e. CC ) ->
      sum_ k e. ( 0 ... N ) ( ( N _C k ) x. ( ( k BernPoly X ) / ( ( N - k ) +
        1 ) ) ) = ( X ^ N ) ) $=
      ( cn0 wcel cc cc0 cfz co cbc cbp cmin c1 caddc cdiv cmul syl wceq oveq12d
      oveq2d wa cv csu cexp cuz cfv simpl nn0uz syl6eleq cz elfzelz bccl syl2an
      nn0cn wne elfznn0 simpr bpolycl syl2anr cn fznn0sub adantlr nn0p1nn nnne0
      divcl syl3anc mulcl syl2anc oveq2 oveq1 oveq1d fsumm1 adantr subid ax-1cn
      nncn bcnn addid2i syl6eq div1 eqtrd mulid2 bpolyval eqcomd wb expcl fzfid
      ancoms cvv ovex fzssp1 ax-mp sylancl syl5sseq sselda syldan fsumcl subadd
      wss npcan mpbid 3eqtrd ) BDEZCFEZUAZGBHIZBAUBZJIZXGCKIZBXGLIZMNIZOIZPIZAU
      CGBMLIZHIZXMAUCZBBJIZBCKIZBBLIZMNIZOIZPIZNIXPXRNIZCBUDIZXEXMYBAGBXEBDGUEU
      FXCXDUGZUHUIXEXGXFEZUAZXHFEZXLFEZXMFEZYGXHDEZYHXEXCXGUJEYKYFYEXGGBUKXGBUL
      UMXHUNQYGXIFEZXKFEZXKGUOZYIYFXGDEXDYLXEXGBUPXCXDUQXGCURUSYGXKUTEZYMYGXJDE
      ZYOXCYFYPXDDXGGBVAVBXJVCQZXKVPQYGYOYNYQXKVDQXIXKVEVFXHXLVGVHZXGBRZXHXQXLY
      APXGBBJVIYSXIXRXKXTOXGBCKVJYSXJXSMNXGBBLVIVKSSVLXEYBXRXPNXEYBMXRPIZXRXEXQ
      MYAXRPXCXQMRXDBVQVMXEYAXRMOIZXRXEXTMXROXEXTGMNIMXEXSGMNXEBFEZXSGRXCUUBXDB
      UNVMZBVNQVKMVOVRVSTXEXRFEZUUAXRRBCURZXRVTQWASXEUUDYTXRRUUEXRWBQWATXEYDXPL
      IZXRRZYCYDRZXEXRUUFABCWCWDXEYDFEZXPFEUUDUUGUUHWEXDXCUUICBWFWHXEXOXMAXEGXN
      WGXEXGXOEYFYJXEXOXFXGXEGXNMNIZHIZXOXFXNWIEXOUUKWSBMLWJGXNWIWKWLXEUUJBGHXE
      UUBMFEUUJBRUUCVOBMWTWMTWNWOYRWPWQUUEYDXPXRWRVFXAXB $.
      $( [22-May-2014] $) $( [16-May-2014] $)
  $}

  ${
    $d k m n N $.  $d k m ph $.  $d k m n X $.
    ${
      bpolydiflem.1 $e |- ( ph -> N e. NN ) $.
      bpolydiflem.2 $e |- ( ph -> X e. CC ) $.
      bpolydiflem.3 $e |- ( ( ph /\ k e. ( 1 ... ( N - 1 ) ) ) ->
                            ( ( k BernPoly ( X + 1 ) ) - ( k BernPoly X ) ) =
                              ( k x. ( X ^ ( k - 1 ) ) ) ) $.
      $( Lemma for ~ bpolydif . $)
      bpolydiflem $p |- ( ph ->
                          ( ( N BernPoly ( X + 1 ) ) - ( N BernPoly X ) ) =
                            ( N x. ( X ^ ( N - 1 ) ) ) ) $=
        ( c1 caddc co cmin cc0 cfz cdiv cmul wcel cc wceq syl syl2anc vm cbp cv
        cexp cbc csu cn0 cn nnnn0 peano2cn bpolyval oveq12d expcl fzfid elfzelz
        wa cz bccl syl2an nn0cn wne elfznn0 bpolycl syl2anr cvv wss ovex fzssp1
        ax-mp ax-1cn npcan sylancl oveq2d syl5sseq sselda fznn0sub sylan syldan
        nncn nn0p1nn nnne0 divcl syl3anc mulcl fsumcl syl22anc c2 addcom oveq1d
        sub4 binom1p eqtrd nn0uz syl6eleq bccl2 oveq2 fsumm1 bcnn mulid2 3eqtrd
        cuz cfv pncan nnm1nn0 subsub4 oveq2i syl6eqr sumeq1d bcnm1 oveq1 fsum1p
        a1i df-2 bpoly0 0z subid1 eqeltrd peano2nn nnrecre recnd fzp1ss sylancr
        cr pnpcan 1z nnz zsubcl oveq1i mp2an eqtri 2cn subsub syl5reqr fsumshft
        2z addid2i adantr syl112anc 3eqtr3d mulass eleq2i bcm1k elfznn sylan2br
        adantl divsubdir div23 divcan6 sylan2b sumeq2dv fsumsub 3eqtr2rd pncan2
        subdi nn0z ) ACDHIJZUBJZCDUBJZKJUUPCUDJZLCHKJZMJZCBUCZUEJZUVBUUPUBJZCUV
        BKJZHIJZNJZOJZBUFZKJZDCUDJZUVAUVCUVBDUBJZUVFNJZOJZBUFZKJZKJZUUSUVKKJZUV
        IUVOKJZKJZCDUUTUDJZOJZAUUQUVJUURUVPKACUGPZUUPQPZUUQUVJRACUHPZUWCECUIZSZ
        ADQPZUWDFDUJSZBCUUPUKTAUWCUWHUURUVPRUWGFBCDUKTULAUUSQPZUVIQPUVKQPZUVOQP
        UVQUVTRAUWDUWCUWJUWIUWGUUPCUMTAUVAUVHBALUUTUNZAUVBUVAPZUPZUVCQPZUVGQPZU
        VHQPZUWNUVCUGPZUWOAUWCUVBUQPUWRUWMUWGUVBLUUTUOUVBCURUSUVCUTSZUWNUVDQPZU
        VFQPZUVFLVAZUWPUWMUVBUGPZUWDUWTAUVBUUTVBZUWIUVBUUPVCVDZUWNUVFUHPZUXAUWN
        UVEUGPZUXFAUWMUVBLCMJZPZUXGAUVAUXHUVBALUUTHIJZMJZUVAUXHUUTVEPZUVAUXKVFC
        HKVGZLUUTVEVHVIAUXJCLMACQPZHQPZUXJCRAUWEUXNECVSZSZVJCHVKVLZVMVNZVOAUWEU
        XIUXGEUHUVBLCVPVQVRUVEVTSZUVFVSSZUWNUXFUXBUXTUVFWASZUVDUVFWBZWCUVCUVGWD
        TZWEAUWHUWCUWKFUWGDCUMTZAUVAUVNBUWLUWNUWOUVMQPZUVNQPZUWSUWNUVLQPZUXAUXB
        UYFUWMUXCUWHUYHAUXDFUVBDVCVDZUYAUYBUVLUVFWBZWCUVCUVMWDTZWEUUSUVIUVKUVOW
        JWFAUVTLCWGKJZMJZCUAUCZUEJZDUYNUDJZOJZUAUFZUWBIJZUYRKJZUWBAUVRUYSUVSUYR
        KAUVRUVAUYQUAUFZLUUTHKJZMJZUYQUAUFZCUUTUEJZUWAOJZIJUYSAUVRVUAUVKIJZUVKK
        JZVUAAUUSVUGUVKKAUUSUXHUYQUAUFZVUACCUEJZUVKOJZIJVUGAUUSHDIJZCUDJZVUIAUU
        PVULCUDAUWHUXOUUPVULRFVJDHWHVLWIAUWHUWCVUMVUIRFUWGDUACWKTWLAUYQVUKUALCA
        CUGLXAXBZUWGWMWNAUYNUXHPZUPZUYOQPZUYPQPZUYQQPZVUPUYOUHPZVUQAUWEVUOVUTEU
        YNCUHWOVQUYOVSSAUWHUYNUGPVURVUOFUYNCVBDUYNUMUSUYOUYPWDTZUYNCRUYOVUJUYPU
        VKOUYNCCUEWPUYNCDUDWPULWQAVUKUVKVUAIAVUKHUVKOJZUVKAVUJHUVKOAUWCVUJHRUWG
        CWRSWIAUWKVVBUVKRUYEUVKWSSWLVMWTWIAVUAQPUWKVUHVUARAUVAUYQUAUWLAUYNUVAPZ
        VUOVUSAUVAUXHUYNUXSVOVVAVRZWEUYEVUAUVKXCTWLAUYQVUFUALUUTAUUTUGVUNAUWEUU
        TUGPZECXDSZWMWNZVVDUYNUUTRUYOVUEUYPUWAOUYNUUTCUEWPUYNUUTDUDWPULWQAVUDUY
        RVUFUWBIAVUCUYMUYQUAAVUBUYLLMAVUBCHHIJZKJZUYLAUXNUXOUXOVUBVVIRUXQUXOAVJ
        XLZVVJCHHXEWCWGVVHCKXMXFXGVMXHAVUECUWAOAUWCVUECRUWGCXISWIULWTAUVSCLUEJZ
        HCLKJZHIJZNJZOJZLHIJZUUTMJZUVHBUFZIJZVVOVVQUVNBUFZIJZKJZVVRVVTKJZUYRAUV
        IVVSUVOVWAKAUVIVVKLUUPUBJZVVMNJZOJZVVRIJVVSAUVHVWFBLUUTVVGUYDUVBLRZUVCV
        VKUVGVWEOUVBLCUEWPZVWGUVDVWDUVFVVMNUVBLUUPUBXJVWGUVEVVLHIUVBLCKWPWIZULU
        LXKAVWFVVOVVRIAVWEVVNVVKOAVWDHVVMNAUWDVWDHRUWIUUPXNSWIVMWIWLAUVOVVKLDUB
        JZVVMNJZOJZVVTIJVWAAUVNVWLBLUUTVVGUYKVWGUVCVVKUVMVWKOVWHVWGUVLVWJUVFVVM
        NUVBLDUBXJVWIULULXKAVWLVVOVVTIAVWKVVNVVKOAVWJHVVMNAUWHVWJHRFDXNSWIVMWIW
        LULAVVOQPZVVRQPVVTQPVWBVWCRAVVKQPZVVNQPVWMAVVKUGPZVWNAUWCLUQPZVWOUWGXOL
        CURVLVVKUTSAVVNAVVMUHPZVVNYCPAVVLUHPVWQAVVLCUHAUXNVVLCRUXQCXPSEXQVVLXRS
        VVMXSSXTVVKVVNWDTAVVQUVHBAVVPUUTUNZAUVBVVQPZUWMUWQAVVQUVAUVBAVWPVVEVVQU
        VAVFXOVVFLUUTUGYAYBVOZUYDVRZWEAVVQUVNBVWRAVWSUWMUYGVWTUYKVRZWEVVOVVRVVT
        YDWCAUYRVVQCUVBHKJZUEJZDVXCUDJZOJZBUFZVVQUVHUVNKJZBUFVWCAUYRVVPUYLHIJZM
        JZVXFBUFVXGAUYQVXFUABHLUYLHUQPAYEXLVWPAXOXLACUQPZWGUQPUYLUQPAUWEVXKECYF
        SYOCWGYGVLAUYNUYMPVVCVUSAUYMUVAUYNALVXIMJZUYMUVAUYLVEPUYMVXLVFCWGKVGLUY
        LVEVHVIAVXIUUTLMAUUTCWGHKJZKJZVXIVXMHCKVXMVVHHKJZHWGVVHHKXMYHUXOUXOVXOH
        RVJVJHHXCYIYJXFAUXNWGQPZUXOVXNVXIRUXQVXPAYKXLVVJCWGHYLWCYMZVMVNVOVVDVRZ
        UYNVXCRUYOVXDUYPVXEOUYNVXCCUEWPUYNVXCDUDWPULYNAVXJVVQVXFBAVXIUUTVVPMVXQ
        VMXHWLAVVQVXHVXFBVWSAUVBHUUTMJZPZVXHVXFRVVQVXSUVBVVPHUUTMHVJYPYHUUAZAVX
        TUPZUVCUVGUVMKJZOJZVXDUVFUVBNJZOJZUVBUVFNJZVXEOJZOJZVXHVXFVYBUVCVYFVYCV
        YHOVYBUVCVXDCVXCKJZUVBNJZOJZVYFVYBUWEUVBHCMJZPUVCVYLRAUWEVXTEYQZAVXSVYM
        UVBAHUXJMJZVXSVYMUXLVXSVYOVFUXMHUUTVEVHVIAUXJCHMUXRVMVNVOUVBCUHUUBTVYBV
        YKVYEVXDOVYBVYJUVFUVBNVYBUXNUVBQPZUXOVYJUVFRVYBUWEUXNVYNUXPSVYBUVBUHPZV
        YPVXTVYQAUVBUUTUUCUUEZUVBVSSZUXOVYBVJXLCUVBHYLWCWIVMWLVYBUVDUVLKJZUVFNJ
        ZUVBVXEOJZUVFNJZVYCVYHVYBVYTWUBUVFNGWIVYBUWTUYHUXAUXBWUAVYCRAVXTUWMUWTV
        XTAVWSUWMVYAVWTUUDZUXEVRZAVXTUWMUYHWUDUYIVRZAVXTUWMUXAWUDUYAVRZAVXTUWMU
        XBWUDUYBVRZUVDUVLUVFUUFYRVYBVYPVXEQPZUXAUXBWUCVYHRVYSVYBUWHVXCUGPZWUIAU
        WHVXTFYQVYBVYQWUJVYRUVBXDSZDVXCUMTZWUGWUHUVBVXEUVFUUGYRYSULVYBUWOUWPUYF
        VYDVXHRAVXTUWMUWOWUDUWSVRVYBUWTUXAUXBUWPWUEWUGWUHUYCWCVYBUYHUXAUXBUYFWU
        FWUGWUHUYJWCUVCUVGUVMUUNWCVYBVYIVXDVYEVYHOJZOJZVXFVYBVXDQPZVYEQPZVYHQPZ
        VYIWUNRVYBVXDUGPZWUOVYBUWCVXCUQPZWURVYBUWEUWCVYNUWFSVYBWUJWUSWUKVXCUUOS
        VXCCURTVXDUTSVYBUXAVYPUVBLVAZWUPWUGVYSVYBVYQWUTVYRUVBWASZUVFUVBWBWCZVYB
        VYGQPZWUIWUQVYBVYPUXAUXBWVCVYSWUGWUHUVBUVFWBWCZWULVYGVXEWDTVXDVYEVYHYTW
        CVYBWUMVXEVXDOVYBVYEVYGOJZVXEOJZHVXEOJZWUMVXEVYBWVEHVXEOVYBUXAUXBVYPWUT
        WVEHRWUGWUHVYSWVAUVFUVBUUHWFWIVYBWUPWVCWUIWVFWUMRWVBWVDWULVYEVYGVXEYTWC
        VYBWUIWVGVXERWULVXEWSSYSVMWLYSUUIUUJAVVQUVHUVNBVWRVXAVXBUUKUULWTULAUYRQ
        PUWBQPZUYTUWBRAUYMUYQUAALUYLUNVXRWEAUXNUWAQPZWVHUXQAUWHVVEWVIFVVFDUUTUM
        TCUWAWDTUYRUWBUUMTWLWT $.
        $( [12-Jun-2014] $)
    $}

    $( Calculate the difference between successive values of the Bernoulli
       polynomials.  (Contributed by Scott Fenton, 16-May-2014.)  (The proof
       was shortened by Mario Carneiro, 26-May-2014.) $)
    bpolydif $p |- ( ( N e. NN /\ X e. CC ) ->
       ( ( N BernPoly ( X + 1 ) ) - ( N BernPoly X ) ) =
       ( N x. ( X ^ ( N - 1 ) ) ) ) $=
      ( vn vk wcel c1 co cbp cmin cexp cmul wceq cv wi oveq1 oveq12d id eqeq12d
      oveq2d imbi2d vm cn cc caddc cfz wral w3a simp1 simp3 wa simpl3 3ad2antl2
      rcla4cva mpd bpolydiflem 3exp nnsinds imp ) AUBEBUCEZABFUDGZHGZABHGZIGZAB
      AFIGZJGZKGZLZUSCMZUTHGZVHBHGZIGZVHBVHFIGZJGZKGZLZNUSDMZUTHGZVPBHGZIGZVPBV
      PFIGZJGZKGZLZNZUSVGNCDAVHVPLZVOWCUSWEVKVSVNWBWEVIVQVJVRIVHVPUTHOVHVPBHOPW
      EVHVPVMWAKWEQWEVLVTBJVHVPFIOSPRTVHALZVOVGUSWFVKVCVNVFWFVIVAVJVBIVHAUTHOVH
      ABHOPWFVHAVMVEKWFQWFVLVDBJVHAFIOSPRTVHUBEZWDDFVLUEGZUFZUSVOWGWIUSUGZUAVHB
      WGWIUSUHWGWIUSUIWJUAMZWHEZUJUSWKUTHGZWKBHGZIGZWKBWKFIGZJGZKGZLZWGWIUSWLUK
      WIWGWLUSWSNZUSWDWTDWKWHVPWKLZWCWSUSXAVSWOWBWRXAVQWMVRWNIVPWKUTHOVPWKBHOPX
      AVPWKWAWQKXAQXAVTWPBJVPWKFIOSPRTUMULUNUOUPUQUR $.
      $( [16-May-2014] $)
  $}

  ${
    $d K k n $.  $d M k n $.
    $( A closed-form expression for the sum of ` K ` -th powers.  (Contributed
       by Scott Fenton, 16-May-2014.)  (Revised by Mario Carneiro,
       16-Jun-2014.) $)
    fsumkthpow $p |- ( ( K e. NN0 /\ M e. NN0 ) ->
       sum_ n e. ( 0 ... M ) ( n ^ K ) =
       ( ( ( ( K + 1 ) BernPoly ( M + 1 ) ) - ( ( K + 1 ) BernPoly 0 ) ) /
         ( K + 1 ) ) ) $=
      ( cn0 wcel wa c1 caddc co cc0 cexp csu cmul cdiv cbp cmin wceq syl oveq2
      cc vk cfz cv wne fzfid cz elfzelz zcn simpl syl2anr fsumcl nn0p1nn adantr
      expcl nncn nnne0 divcan3 syl3anc fsummulc2 bpolydif syl2an nn0cn ad2antrr
      cn ax-1cn pncan sylancl oveq2d eqtrd sumeq2dv nn0z adantl peano2nn0 nn0uz
      cuz cfv syl6eleq elfznn0 bpolycl syl2anc fsumtscop2 3eqtr2d oveq1d eqtr3d
      ) BDEZCDEZFZBGHIZJCUBIZAUCZBKIZALZMIZWHNIZWLWHCGHIZOIZWHJOIZPIZWHNIWGWLTE
      WHTEZWHJUDZWNWLQWGWIWKAWGJCUEZWJWIEZWJTEZWEWKTEWGXBWJUFEXCWJJCUGWJUHRZWEW
      FUIWJBUNUJZUKWGWHVDEZWSWEXFWFBULUMZWHUORZWGXFWTXGWHUPRWLWHUQURWGWMWRWHNWG
      WMWIWHWKMIZALWIWHWJGHIZOIZWHWJOIZPIZALWRWGWIWKWHAXAXHXEUSWGWIXMXIAWGXBFZX
      MWHWJWHGPIZKIZMIZXIWGXFXCXMXQQXBXGXDWHWJUTVAXNXPWKWHMXNXOBWJKXNBTEZGTEXOB
      QWEXRWFXBBVBVCVEBGVFVGVHVHVIVJWGWHUAUCZOIZXLXKWQAUAWPJCXSWJWHOSXSXJWHOSXS
      JWHOSXSWOWHOSWFCUFEWECVKVLWGWODJVOVPWFWODEWECVMVLVNVQWGXSJWOUBIEZFZWHDEZX
      STEZXTTEWEYCWFYABVMVCYBXSDEZYDYAYEWGXSWOVRVLXSVBRWHXSVSVTWAWBWCWD $.
      $( [16-Jun-2014] $) $( [16-May-2014] $)
  $}

$( (End of Scott Fenton's mathbox.) $)


