$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Birthday Function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f x y $.
    $( The birthday function maps the surreals onto the ordinals.  Alling's
       axiom (B).  (Shortened proof on 2012-Apr-14, SF).  (Contributed by Scott
       Fenton, 11-Jun-2011.) $)
    axbday $p |- bday : No -onto-> On $=
      ( vx vy csur con0 cbday wfo wfn crn wceq df-fo cv cdm cvv wcel wral dmexg
      rgen df-bday mptfng c1o mpbi wrex cab rnmpt csn cxp noxp1o wne elexi snnz
      c0 1on dmxp ax-mp eqcomi dmeq eqeq2d rcla4ev sylancl wi nodmon eleq1a syl
      rexlimiv impbii abbi2i eqtr4i mpbir2an ) CDEFECGZEHZDICDEJAKZLZMNZACOVIVM
      ACVKCPQACVLEARZSUAVJBKZVLIZACUBZBUCDABCVLEVNUDVQBDVODNZVQVRVOTUEZUFZCNVOV
      TLZIZVQVOUGWAVOVSUKUHWAVOITTDULUIUJVOVSUMUNUOVPWBAVTCVKVTIVLWAVOVKVTUPUQU
      RUSVPVRACVKCNVLDNVPVRUTVKVAVLDVOVBVCVDVEVFVGVH $.
      $( [12-May-2012] $) $( [11-Jun-2011] $)

  $}

  $( The birthday function is a function.  (Contributed by Scott Fenton,
     14-Jun-2011.) $)
  bdayfun $p |- Fun bday $=
    ( csur con0 cbday wfo wfun axbday fofun ax-mp ) ABCDCEFABCGH $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function's range is ` On ` (Contributed by Scott Fenton,
     14-Jun-2011.) $)
  bdayrn $p |- ran bday = On $=
    ( csur con0 cbday wfo crn wceq axbday forn ax-mp ) ABCDCEBFGABCHI $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function's domain is ` No ` (Contributed by Scott Fenton,
     14-Jun-2011.) $)
  bdaydm $p |- dom bday = No $=
    ( csur con0 cbday wfo wf axbday fof ax-mp fdmi ) ABCABCDABCEFABCGHI $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function is a function over ` No ` (Contributed by Scott
     Fenton, 30-Jun-2011.) $)
  bdayfn $p |- bday Fn No $=
    ( cbday csur wfn wfun cdm wceq df-fn bdayfun bdaydm mpbir2an ) ABCADAEBFABG
    HIJ $.
    $( [10-Jul-2011] $) $( [30-Jun-2011] $)

  $( The value of the birthday function is always an ordinal.  (Contributed by
     Scott Fenton, 14-Jun-2011.) $)
  bdayelon $p |- ( bday ` A ) e. On $=
    ( cbday cdm wcel cfv con0 wfun bdayfun wa fvelrn bdayrn syl6eleq mpan wn c0
    crn ndmfv 0elon syl6eqel pm2.61i ) ABCDZABEZFDZBGZUAUCHUDUAIUBBPFABJKLMUANU
    BOFABQRST $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The surreal numbers are a proper class.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  noprc $p |- -. No e. _V $=
    ( csur cvv wcel con0 onprc cbday wfo axbday fornex mpi mto ) ABCZDBCZELADFG
    MHADBFIJK $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

