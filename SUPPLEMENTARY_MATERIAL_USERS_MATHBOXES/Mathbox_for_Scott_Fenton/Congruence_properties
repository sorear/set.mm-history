$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Congruence properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c OuterFiveSeg $.

  $( Declare the syntax for the outer five segment configuration. $)
  cofs $a class OuterFiveSeg $.

  ${
    $d a b c d x y z w p q n $.
    $( The outer five segment configuration is an abbreviation for the
       conditions of the Five Segment Axiom ( ~ ax5seg ).  See ~ brofs and
       ~ 5segofs for how it is used.  Definition 2.10 of [Schwabhauser]
       p. 28. $)
    df-ofs $a |- OuterFiveSeg =
        { <. p , q >. |
           E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
           E. d e. ( EE ` n ) E. x e. ( EE ` n ) E. y e. ( EE ` n )
           E. z e. ( EE ` n ) E. w e. ( EE ` n ) (
           p = <. <. a , b >. , <. c , d >. >. /\
           q = <. <. x , y >. , <. z , w >. >. /\
           ( ( b Btwn <. a , c >. /\ y Btwn <. x , z >. ) /\
             ( <. a , b >. Cgr <. x , y >. /\
               <. b , c >. Cgr <. y , z >. ) /\
             ( <. a , d >. Cgr <. x , w >. /\
               <. b , d >. Cgr <. y , w >. ) ) ) } $.
  $}

  ${
    cgrrflx2d.1 $e |- ( ph -> N e. NN ) $.
    cgrrflx2d.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrrflx2d.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    $( Deduction form of ~ axcgrrflx .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrrflx2d $p |- ( ph -> <. A , B >. Cgr <. B , A >. ) $=
      ( cn wcel cee cfv cop ccgr wbr axcgrrflx syl3anc ) ADHIBDJKZICQIBCLCBLMNE
      FGBCDOP $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  ${
    cgrtr4d.1 $e |- ( ph -> N e. NN ) $.
    cgrtr4d.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrtr4d.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrtr4d.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrtr4d.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrtr4d.6 $e |- ( ph -> E e. ( EE ` N ) ) $.
    cgrtr4d.7 $e |- ( ph -> F e. ( EE ` N ) ) $.
    cgrtr4d.8 $e |- ( ph -> <. A , B >. Cgr <. C , D >. ) $.
    cgrtr4d.9 $e |- ( ph -> <. A , B >. Cgr <. E , F >. ) $.
    $( Deduction form of ~ axcgrtr .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrtr4d $p |- ( ph -> <. C , D >. Cgr <. E , F >. ) $=
      ( cop ccgr wcel wbr cn cee cfv wa wi axcgrtr syl133anc mp2and ) ABCRZDERZ
      SUAZUJFGRZSUAZUKUMSUAZPQAHUBTBHUCUDZTCUPTDUPTEUPTFUPTGUPTULUNUEUOUFIJKLMN
      OBCDEFGHUGUHUI $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  ${
    cgrtr4and.1 $e |- ( ph -> N e. NN ) $.
    cgrtr4and.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrtr4and.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrtr4and.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrtr4and.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrtr4and.6 $e |- ( ph -> E e. ( EE ` N ) ) $.
    cgrtr4and.7 $e |- ( ph -> F e. ( EE ` N ) ) $.
    cgrtr4and.8 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. C , D >. ) $.
    cgrtr4and.9 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. E , F >. ) $.
    $( Deduction form of ~ axcgrtr .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrtr4and $p |- ( ( ph /\ ps ) -> <. C , D >. Cgr <. E , F >. ) $=
      ( wcel adantr wa cn cee cfv cgrtr4d ) ABUACDEFGHIAIUBSBJTACIUCUDZSBKTADUF
      SBLTAEUFSBMTAFUFSBNTAGUFSBOTAHUFSBPTQRUE $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  $( Reflexivity law for congruence.  Theorem 2.1 of [Schwabhauser] p. 27.
     (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrrflx $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    <. A , B >. Cgr <. A , B >. ) $=
    ( wcel cee cfv w3a simp1 simp3 simp2 cop ccgr wbr axcgrrflx 3com23 cgrtr4d
    cn ) CQDZACEFZDZBSDZGBAABABCRTUAHRTUAIZRTUAJZUCUBUCUBRUATBAKABKLMBACNOZUDP
    $.
    $( [12-Jun-2013] $)

  ${
    cgrrflxd.1 $e |- ( ph -> N e. NN ) $.
    cgrrflxd.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrrflxd.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    $( Deduction form of ~ cgrrflx .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrrflxd $p |- ( ph -> <. A , B >. Cgr <. A , B >. ) $=
      ( cn wcel cee cfv cop ccgr wbr cgrrflx syl3anc ) ADHIBDJKZICQIBCLZRMNEFGB
      CDOP $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  $( Congruence commutes on the two sides.  Implication version.  Theorem 2.2
     of [Schwabhauser] p. 27.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrcomim $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. ->
      <. C , D >. Cgr <. A , B >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr wbr simp1 simp2l simp2r simp3l simp3r simpr
    wa simpl1 simpl2l simpl2r cgrrflxd cgrtr4and ex ) EFGZAEHIZGZBUHGZTZCUHGZDU
    HGZTZJZABKZCDKZLMZUQUPLMUOURABCDABEUGUKUNNUGUIUJUNOZUGUIUJUNPZUGUKULUMQUGUK
    ULUMRUSUTUOURSUOURTABEUGUKUNURUAUIUJUGUNURUBUIUJUGUNURUCUDUEUF $.
    $( [12-Jun-2013] $)

  $( Congruence commutes between the two sides.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcom $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. C , D >. Cgr <. A , B >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr cgrcomim wi 3com23 impbid ) EFGZAEHIZ
    GBTGJZCTGDTGJZKABLZCDLZMNZUDUCMNZABCDEOSUBUAUFUEPCDABEOQR $.
    $( [12-Jun-2013] $)

  ${
    cgrcomand.1 $e |- ( ph -> N e. NN ) $.
    cgrcomand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrcomand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrcomand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrcomand.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrcomand.6 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. C , D >. ) $.
    $( Deduction form of ~ cgrcom .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrcomand $p |- ( ( ph /\ ps ) -> <. C , D >. Cgr <. A , B >. ) $=
      ( wa cop ccgr wbr wb cn wcel cee cfv cgrcom syl122anc adantr mpbid ) ABNC
      DOZEFOZPQZUHUGPQZMAUIUJRZBAGSTCGUAUBZTDULTEULTFULTUKHIJKLCDEFGUCUDUEUF $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  $( Transitivity law for congruence.  Theorem 2.3 of [Schwabhauser] p. 27.
     (Contributed by Scott Fenton, 24-Sep-2013.) $)
  cgrtr $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. Cgr <. C , D >. /\
           <. C , D >. Cgr <. E , F >. ) ->
         <. A , B >. Cgr <. E , F >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr wbr wa simp1 simp23 simp31 simp21 cgrcomand
    simp22 simp32 simp33 simprl simprr cgrtr4and ex ) GHIZAGJKZIZBUJIZCUJIZLZDU
    JIZEUJIZFUJIZLZLZABMZCDMZNOZVAEFMZNOZPZUTVCNOUSVECDABEFGUIUNURQZUIUKULUMURR
    ZUIUNUOUPUQSZUIUKULUMURTZUIUKULUMURUBZUIUNUOUPUQUCUIUNUOUPUQUDUSVEABCDGVFVI
    VJVGVHUSVBVDUEUAUSVBVDUFUGUH $.
    $( [14-Oct-2013] $) $( [24-Sep-2013] $)

  ${
    cgrtrand.1 $e |- ( ph -> N e. NN ) $.
    cgrtrand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrtrand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrtrand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrtrand.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrtrand.6 $e |- ( ph -> E e. ( EE ` N ) ) $.
    cgrtrand.7 $e |- ( ph -> F e. ( EE ` N ) ) $.
    cgrtrand.8 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. C , D >. ) $.
    cgrtrand.9 $e |- ( ( ph /\ ps ) -> <. C , D >. Cgr <. E , F >. ) $.
    $( Deduction form of ~ cgrtr .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrtrand $p |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. E , F >. ) $=
      ( cop wcel wa ccgr wbr wi cn cee cfv cgrtr syl133anc adantr mp2and ) ABUA
      CDSZEFSZUBUCZUMGHSZUBUCZULUOUBUCZQRAUNUPUAUQUDZBAIUETCIUFUGZTDUSTEUSTFUST
      GUSTHUSTURJKLMNOPCDEFGHIUHUIUJUK $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  $( Transitivity law for congruence.  (Contributed by Scott Fenton,
     7-Oct-2013.) $)
  cgrtr3 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. Cgr <. E , F >. /\
           <. C , D >. Cgr <. E , F >. ) ->
         <. A , B >. Cgr <. C , D >. ) ) $=
    ( cn wcel cee cfv w3a cop ccgr wbr wa simp1 simp21 simp22 simp32 cgrcomand
    simp33 simp23 simp31 simprl simprr cgrtrand ex ) GHIZAGJKZIZBUJIZCUJIZLZDUJ
    IZEUJIZFUJIZLZLZABMZEFMZNOZCDMZVANOZPZUTVCNOUSVEABEFCDGUIUNURQZUIUKULUMURRU
    IUKULUMURSUIUNUOUPUQTZUIUNUOUPUQUBZUIUKULUMURUCZUIUNUOUPUQUDZUSVBVDUEUSVECD
    EFGVFVIVJVGVHUSVBVDUFUAUGUH $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  ${
    cgrtr3and.1 $e |- ( ph -> N e. NN ) $.
    cgrtr3and.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrtr3and.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrtr3and.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrtr3and.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrtr3and.6 $e |- ( ph -> E e. ( EE ` N ) ) $.
    cgrtr3and.7 $e |- ( ph -> F e. ( EE ` N ) ) $.
    cgrtr3and.8 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. E , F >. ) $.
    cgrtr3and.9 $e |- ( ( ph /\ ps ) -> <. C , D >. Cgr <. E , F >. ) $.
    $( Deduction form of ~ cgrtr3 .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    cgrtr3and $p |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. C , D >. ) $=
      ( cop wcel wa ccgr wbr wi cn cee cfv cgrtr3 syl133anc adantr mp2and ) ABU
      ACDSZGHSZUBUCZEFSZUMUBUCZULUOUBUCZQRAUNUPUAUQUDZBAIUETCIUFUGZTDUSTEUSTFUS
      TGUSTHUSTURJKLMNOPCDEFGHIUHUIUJUK $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  $( Congruence commutes on the left.  Biconditional version of Theorem 2.4 of
     [Schwabhauser] p. 27.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrcoml $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. B , A >. Cgr <. C , D >. ) ) $=
    ( cn wcel cee cfv wa w3a cop wbr simp1 cgrrflx2d wi axcgrtr syl133anc mpand
    ccgr simp2l simp2r simp3l simp3r impbid ) EFGZAEHIZGZBUGGZJZCUGGZDUGGZJZKZA
    BLZCDLZTMZBALZUPTMZUNUOURTMZUQUSUNABEUFUJUMNZUFUHUIUMUAZUFUHUIUMUBZOUNUFUHU
    IUIUHUKULUTUQJUSPVAVBVCVCVBUFUJUKULUCZUFUJUKULUDZABBACDEQRSUNURUOTMZUSUQUNB
    AEVAVCVBOUNUFUIUHUHUIUKULVFUSJUQPVAVCVBVBVCVDVEBAABCDEQRSUE $.
    $( [12-Jun-2013] $)

  $( Congruence commutes on the right.  Biconditional version of Theorem 2.5 of
     [Schwabhauser] p. 27.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrcomr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. A , B >. Cgr <. D , C >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr wb cgrcoml 3com23 cgrcom simp1 simp2l
    simp2r simp3r simp3l syl122anc 3bitr4d ) EFGZAEHIZGZBUGGZJZCUGGZDUGGZJZKZCD
    LZABLZMNZDCLZUPMNZUPUOMNUPURMNZUFUMUJUQUSOCDABEPQABCDERUNUFUHUIULUKUTUSOUFU
    JUMSUFUHUIUMTUFUHUIUMUAUFUJUKULUBUFUJUKULUCABDCERUDUE $.
    $( [12-Jun-2013] $)

  $( Congruence commutes on both sides.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcomlr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. B , A >. Cgr <. D , C >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr cgrcoml ancom cgrcomr syl3an2b bitrd
    wb ) EFGZAEHIZGZBUBGZJZCUBGDUBGJZKABLCDLZMNBALZUGMNZUHDCLMNZABCDEOUEUAUDUCJ
    UFUIUJTUCUDPBACDEQRS $.
    $( [12-Jun-2013] $)

  ${
    cgrcomlrand.1 $e |- ( ph -> N e. NN ) $.
    cgrcomlrand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrcomlrand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrcomlrand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrcomlrand.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrcomlrand.6 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. C , D >. ) $.
    $( Deduction form of ~ cgrcoml .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    cgrcomland $p |- ( ( ph /\ ps ) -> <. B , A >. Cgr <. C , D >. ) $=
      ( wa cop ccgr wbr wb cn wcel cee cfv cgrcoml syl122anc adantr mpbid ) ABN
      CDOEFOZPQZDCOUGPQZMAUHUIRZBAGSTCGUAUBZTDUKTEUKTFUKTUJHIJKLCDEFGUCUDUEUF
      $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)

    $( Deduction form of ~ cgrcoml .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    cgrcomrand $p |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. D , C >. ) $=
      ( wa cop ccgr wbr wb cn wcel cee cfv cgrcomr syl122anc adantr mpbid ) ABN
      CDOZEFOPQZUGFEOPQZMAUHUIRZBAGSTCGUAUBZTDUKTEUKTFUKTUJHIJKLCDEFGUCUDUEUF
      $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)

    $( Deduction form of ~ cgrcomlr .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    cgrcomlrand $p |- ( ( ph /\ ps ) -> <. B , A >. Cgr <. D , C >. ) $=
      ( cgrcomrand cgrcomland ) ABCDFEGHIJLKABCDEFGHIJKLMNO $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)

  $}

  ${
    $d A x $.  $d B x $.  $d N x $.
    $( Degenerate segments are congruent.  Theorem 2.8 of [Schwabhauser]
       p. 28.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
    cgrtriv $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    <. A , A >. Cgr <. B , B >. ) $=
      ( vx cn wcel cee cfv w3a cv cop cbtwn wbr ccgr simp1 simp2 simp3 axsegcon
      wa wrex syl122anc wceq simpl1 simpl2 simpr simpl3 axcgrid syl13anc breq1d
      wi opeq2 biimprd syli adantld rexlimdva mpd ) CEFZACGHZFZBURFZIZAADJZKZLM
      ZVCBBKZNMZSZDURTZAAKZVENMZVAUQUSUSUTUTVHUQUSUTOUQUSUTPZVKUQUSUTQZVLDAABBC
      RUAVAVGVJDURVAVBURFZSZVFVJVDVFVNAVBUBZVJVNUQUSVMUTVFVOUJUQUSUTVMUCUQUSUTV
      MUDVAVMUEUQUSUTVMUFAVBBCUGUHVOVJVFVOVIVCVENAVBAUKUIULUMUNUOUP $.
      $( [12-Jun-2013] $)
  $}

  $( Identity law for congruence.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrid2 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( <. A , A >. Cgr <. B , C >. -> B = C ) ) $=
    ( cn wcel cee cfv w3a wa cop ccgr wceq wb simpl simpr1 simpr2 simpr3 cgrcom
    wbr syl122anc wi 3anrot axcgrid sylan2b sylbid ) DEFZADGHZFZBUHFZCUHFZIZJZA
    AKZBCKZLTZUOUNLTZBCMZUMUGUIUIUJUKUPUQNUGULOUGUIUJUKPZUSUGUIUJUKQUGUIUJUKRAA
    BCDSUAULUGUJUKUIIUQURUBUIUJUKUCBCADUDUEUF $.
    $( [12-Jun-2013] $)

  $( Two congruent segments are either both degenrate or both non-degenerate.
     (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrdegen $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. Cgr <. C , D >. -> ( A = B <-> C = D ) ) ) $=
    ( cn wcel cee cfv wa cop ccgr wbr wceq opeq1 biimpac syl13anc syl5 expdimp
    wi wb breq1d simp1 simp2r simp3l simp3r cgrid2 breq2d simp2l axcgrid impbid
    w3a ex ) EFGZAEHIZGZBUOGZJZCUOGZDUOGZJZULZABKZCDKZLMZABNZCDNZUAVBVEJVFVGVBV
    EVFVGVEVFJBBKZVDLMZVBVGVFVEVIVFVCVHVDLABBOUBPVBUNUQUSUTVIVGTUNURVAUCZUNUPUQ
    VAUDZUNURUSUTUEUNURUSUTUFZBCDEUGQRSVBVEVGVFVEVGJVCDDKZLMZVBVFVGVEVNVGVDVMVC
    LCDDOUHPVBUNUPUQUTVNVFTVJUNUPUQVAUIVKVLABDEUJQRSUKUM $.
    $( [12-Jun-2013] $)

  ${
    $d N a b c d e f g h p q n $.  $d A a b c d e f g h p q n $.
    $d B a b c d e f g h p q n $.  $d C a b c d e f g h p q n $.
    $d D a b c d e f g h p q n $.  $d E a b c d e f g h p q n $.
    $d F a b c d e f g h p q n $.  $d G a b c d e f g h p q n $.
    $d H a b c d e f g h p q n $.
    $( Binary relationship form of the outer five segment predicate.
       (Contributed by Scott Fenton, 21-Sep-2013.) $)
    brofs $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. OuterFiveSeg
      <. <. E , F >. , <. G , H >. >. <-> ( ( B Btwn <. A , C >. /\
                     F Btwn <. E , G >. ) /\
        ( <. A , B >. Cgr <. E , F >. /\
          <. B , C >. Cgr <. F , G >. ) /\
        ( <. A , D >. Cgr <. E , H >. /\
          <. B , D >. Cgr <. F , H >. ) ) ) ) $=
      ( cv cop cbtwn wbr wa ccgr w3a wceq opeq1 breq2d opeq2 vb va vc vf ve cee
      vg vd vh vn vq vp cfv cofs anbi1d breq1d 3anbi123d breq1 anbi12d 3anbi12d
      cn anbi2d 3anbi3d fveq2 df-ofs br8 ) UAJZUBJZUCJZKZLMZUDJZUEJZUGJZKZLMZNZ
      VHVGKZVMVLKZOMZVGVIKZVLVNKZOMZNZVHUHJZKZVMUIJZKZOMZVGWEKZVLWGKZOMZNZPVGAV
      IKZLMZVPNZAVGKZVSOMZWCNZAWEKZWHOMZWLNZPBWNLMZVPNZABKZVSOMZBVIKZWBOMZNZXAB
      WEKZWKOMZNZPBACKZLMZVPNZXFBCKZWBOMZNZXLPXOXRADKZWHOMZBDKZWKOMZNZPXNVLEVNK
      ZLMZNZXEEVLKZOMZXQNZXSEWGKZOMZYBNZPXNFYDLMZNZXEEFKZOMZXPFVNKZOMZNZYKYAFWG
      KZOMZNZPXNFEGKZLMZNZYPXPFGKZOMZNZUUBPUUEUUHXSEHKZOMZYAFHKZOMZNZPUJABCDUJJ
      ZUFUMIUFUMUNVAUEUDUGUIEFGHIUKULUBUAUCUHVHAQZVQWPWDWSWMXBUUOVKWOVPUUOVJWNV
      GLVHAVIRSUOUUOVTWRWCUUOVRWQVSOVHAVGRUPUOUUOWIXAWLUUOWFWTWHOVHAWERUPUOUQVG
      BQZWPXDWSXIXBXLUUPWOXCVPVGBWNLURUOUUPWRXFWCXHUUPWQXEVSOVGBATUPUUPWAXGWBOV
      GBVIRUPUSUUPWLXKXAUUPWJXJWKOVGBWERUPVBUQVICQZXDXOXIXRXLUUQXCXNVPUUQWNXMBL
      VICATSUOUUQXHXQXFUUQXGXPWBOVICBTUPVBUTWEDQZXLYCXOXRUURXAXTXKYBUURWTXSWHOW
      EDATUPUURXJYAWKOWEDBTUPUSVCVMEQZXOYFXRYIYCYLUUSVPYEXNUUSVOYDVLLVMEVNRSVBU
      USXFYHXQUUSVSYGXEOVMEVLRSUOUUSXTYKYBUUSWHYJXSOVMEWGRSUOUQVLFQZYFYNYIYSYLU
      UBUUTYEYMXNVLFYDLURVBUUTYHYPXQYRUUTYGYOXEOVLFETSUUTWBYQXPOVLFVNRSUSUUTYBU
      UAYKUUTWKYTYAOVLFWGRSVBUQVNGQZYNUUEYSUUHUUBUVAYMUUDXNUVAYDUUCFLVNGETSVBUV
      AYRUUGYPUVAYQUUFXPOVNGFTSVBUTWGHQZUUBUUMUUEUUHUVBYKUUJUUAUULUVBYJUUIXSOWG
      HETSUVBYTUUKYAOWGHFTSUSVCUUNIUFVDUEUDUGUIUJUKULUBUAUCUHVEVF $.
      $( [14-Oct-2013] $) $( [21-Sep-2013] $)
  $}

  $( Rephrase ~ ax5seg using the outer five segment predicate.  Theorem 2.10 of
     [Schwabhauser] p. 28.  (Contributed by Scott Fenton, 21-Sep-2013.) $)
  5segofs $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( ( <. <. A , B >. , <. C , D >. >. OuterFiveSeg
        <. <. E , F >. , <. G , H >. >. /\ A =/= B ) ->
        <. C , D >. Cgr <. G , H >. ) ) $=
    ( cn wcel cee cfv w3a cop wbr wa cbtwn ccgr 3jca brofs anbi1d simpr simpl1l
    cofs wne simpl1r simpl2 simpl3 syl6bi ax5seg syld ) IJKAILMZKBUMKNCUMKDUMKE
    UMKNFUMKGUMKHUMKNNZABOZCDOZOEFOZGHOZOUEPZABUFZQZUTBACORPZFEGORPZNZUOUQSPBCO
    FGOSPQZADOEHOSPBDOFHOSPQZNZUPURSPUNVAVBVCQZVEVFNZUTQZVGUNUSVIUTABCDEFGHIUAU
    BVJVDVEVFVJUTVBVCVIUTUCVBVCVEVFUTUDVBVCVEVFUTUGTVHVEVFUTUHVHVEVFUTUITUJABCD
    EFGHIUKUL $.
    $( [14-Oct-2013] $) $( [21-Sep-2013] $)

  $( The outer five segment predicate commutes.  (Contributed by Scott Fenton,
     26-Sep-2013.) $)
  ofscom $p |- ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
        ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
    ( <. <. A , B >. , <. C , D >. >. OuterFiveSeg
      <. <. E , F >. , <. G , H >. >. <->
      <. <. E , F >. , <. G , H >. >. OuterFiveSeg
      <. <. A , B >. , <. C , D >. >. ) ) $=
    ( wcel w3a cop cbtwn wbr wa ccgr cofs wb cgrcom syl122anc cee cfv ancom a1i
    cn simp11 simp12 simp13 simp23 simp31 simp21 simp32 simp22 simp33 3anbi123d
    anbi12d brofs syl333anc 3bitr4d ) IUEJZAIUAUBZJZBVAJZKZCVAJZDVAJZEVAJZKZFVA
    JZGVAJZHVAJZKZKZBACLMNZFEGLMNZOZABLZEFLZPNZBCLZFGLZPNZOZADLZEHLZPNZBDLZFHLZ
    PNZOZKVOVNOZVRVQPNZWAVTPNZOZWEWDPNZWHWGPNZOZKZVQCDLLZVRGHLLZQNWTWSQNZVMVPWK
    WCWNWJWQVPWKRVMVNVOUCUDVMVSWLWBWMVMUTVBVCVGVIVSWLRUTVBVCVHVLUFZUTVBVCVHVLUG
    ZUTVBVCVHVLUHZVDVEVFVGVLUIZVDVHVIVJVKUJZABEFISTVMUTVCVEVIVJWBWMRXBXDVDVEVFV
    GVLUKZXFVDVHVIVJVKULZBCFGISTUPVMWFWOWIWPVMUTVBVFVGVKWFWORXBXCVDVEVFVGVLUMZX
    EVDVHVIVJVKUNZADEHISTVMUTVCVFVIVKWIWPRXBXDXIXFXJBDFHISTUPUOABCDEFGHIUQVMUTV
    GVIVJVKVBVCVEVFXAWRRXBXEXFXHXJXCXDXGXIEFGHABCDIUQURUS $.
    $( [14-Oct-2013] $) $( [26-Sep-2013] $)

  $( Link congruence over a pair of line segments.  Theorem 2.11 of
     [Schwabhauser] p. 29.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrextend $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
    ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
    ( ( ( B Btwn <. A , C >. /\ E Btwn <. D , F >. ) /\
        ( <. A , B >. Cgr <. D , E >. /\
          <. B , C >. Cgr <. E , F >. ) ) ->
      <. A , C >. Cgr <. D , F >. ) ) $=
    ( wcel w3a cop cbtwn wbr wa ccgr wi wceq wb opeq1 adantr 3jca cn cee breq1d
    cfv simp1 simp22 simp31 simp32 cgrid2 syl13anc adantl sylbid breqan12d syld
    exbiri imp3a adantld ex cofs simpl1 simpl21 simpl22 simpl23 simpl31 simpl32
    wne simpl33 simprrl simprrr cgrtriv syl3anc simpld cgrcomlr syl122anc mpbid
    jca brofs syl333anc mpbir3and simprl 5segofs sylc exp32 com12 pm2.61ine ) G
    UAHZAGUBUDZHZBWGHZCWGHZIZDWGHZEWGHZFWGHZIZIZBACJZKLEDFJZKLMZABJZDEJZNLZBCJZ
    EFJZNLZMZMZWQWRNLZOZOABABPZWPXIXJWPMZXFXHWSXKXBXEXHXKXBDEPZXEXHOZXKXBBBJZXA
    NLZXLXJXBXOQWPXJWTXNXANABBRUCSWPXOXLOZXJWPWFWIWLWMXPWFWKWOUEWFWHWIWJWOUFWFW
    KWLWMWNUGWFWKWLWMWNUHBDEGUIUJUKULXJXLXMOWPXJXLXHXEXJXLWQXCWRXDNABCRDEFRUMUO
    SUNUPUQURWPABVFZXIWPXQXGXHWPXQXGMZMZCAJZFDJZNLZXHXSWFWHWIIZWJWHWLIZWMWNWLIZ
    IWTXTJXAYAJUSLZXQMYBXSYCYDYEXSWFWHWIWFWKWOXRUTZWHWIWJWFWOXRVAZWHWIWJWFWOXRV
    BZTXSWJWHWLWHWIWJWFWOXRVCZYHWLWMWNWFWKXRVDZTXSWMWNWLWLWMWNWFWKXRVEZWLWMWNWF
    WKXRVGZYKTTXSYFXQXSYFWSXFAAJDDJNLZBAJEDJNLZMZWPXQWSXFVHWPXQWSXFVIZXSYNYOXSW
    FWHWLYNYGYHYKADGVJVKXSXBYOXSXBXEYQVLXSWFWHWIWLWMXBYOQYGYHYIYKYLABDEGVMVNVOV
    PXSWFWHWIWJWHWLWMWNWLYFWSXFYPIQYGYHYIYJYHYKYLYMYKABCADEFDGVQVRVSWPXQXGVTVPA
    BCADEFDGWAWBXSWFWJWHWNWLYBXHQYGYJYHYMYKCAFDGVMVNVOWCWDWE $.
    $( [12-Jun-2013] $)

  ${
    cgrextendand.1 $e |- ( ph -> N e. NN ) $.
    cgrextendand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    cgrextendand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    cgrextendand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    cgrextendand.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    cgrextendand.6 $e |- ( ph -> E e. ( EE ` N ) ) $.
    cgrextendand.7 $e |- ( ph -> F e. ( EE ` N ) ) $.
    cgrextendand.8 $e |- ( ( ph /\ ps ) -> B Btwn <. A , C >. ) $.
    cgrextendand.9 $e |- ( ( ph /\ ps ) -> E Btwn <. D , F >. ) $.
    cgrextendand.10 $e |- ( ( ph /\ ps ) -> <. A , B >. Cgr <. D , E >. ) $.
    cgrextendand.11 $e |- ( ( ph /\ ps ) -> <. B , C >. Cgr <. E , F >. ) $.
    $( Deduction form of ~ cgrextend .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    cgrextendand $p |- ( ( ph /\ ps ) -> <. A , C >. Cgr <. D , F >. ) $=
      ( wa cop cbtwn wbr ccgr jca wi cn wcel cee cfv cgrextend syl133anc adantr
      mp2and ) ABUAZDCEUBZUCUDZGFHUBZUCUDZUAZCDUBFGUBUEUDZDEUBGHUBUEUDZUAZUQUSU
      EUDZUPURUTQRUFUPVBVCSTUFAVAVDUAVEUGZBAIUHUICIUJUKZUIDVGUIEVGUIFVGUIGVGUIH
      VGUIVFJKLMNOPCDEFGHIULUMUNUO $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

  $( Two points that satsify the conclusion of ~ axsegcon are identical.
     Uniqueness portion of Theorem 2.12 of [Schwabhauser] p. 29.  (Contributed
     by Scott Fenton, 12-Jun-2013.) $)
  segconeq $p |- ( ( N e. NN /\
   ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
   ( Q e. ( EE ` N ) /\ X e. ( EE ` N ) /\ Y e. ( EE ` N ) ) ) ->
   ( ( Q =/= A /\
     ( A Btwn <. Q , X >. /\ <. A , X >. Cgr <. B , C >. ) /\
     ( A Btwn <. Q , Y >. /\ <. A , Y >. Cgr <. B , C >. ) )
   -> X = Y ) ) $=
    ( wcel w3a cop cbtwn wbr ccgr wa jca cgrrflxd 3jca wb cgrcom wi cee cfv wne
    cn cofs wceq simpr2l simpl1 simpl31 simpl21 simpl32 simpl33 simpr3l simpl22
    simpl23 simpr3r syl122anc mpbid simpr2r cgrtr4d jca32 cgrextend sylc simp31
    simp1 simp21 simp32 simp33 brofs syl333anc sylibrd a1i jcad 5segofs axcgrid
    ex syl13anc 3syld ) EUDHZAEUAUBZHZBVTHZCVTHZIZDVTHZFVTHZGVTHZIZIZDAUCZADFJZ
    KLZAFJZBCJZMLZNZADGJZKLZAGJZWNMLZNZIZDAJZFGJZJXCFFJZJUELZWJNZXDXEMLZFGUFZWI
    XBXFWJWIXBWLWLNZXCXCMLZWMWMMLZNZWQWKMLZWSWMMLZNZIZXFWIXBXQWIXBNZXJXMXPXRWLW
    LWLWOWJXAWIUGZXSOXRXKXLXRDAEVSWDWHXBUHZWEWFWGVSWDXBUIZWAWBWCVSWHXBUJZPZXRAF
    EXTYBWEWFWGVSWDXBUKZPOXRXNXOXRVSWEWAWGIZWEWAWFIZIWRWLNZXKXONNXNXRVSYEYFXTXR
    WEWAWGYAYBWEWFWGVSWDXBULZQXRWEWAWFYAYBYDQQXRYGXKXOXRWRWLWRWTWJWPWIUMXSOYCXR
    BCAGAFEXTWAWBWCVSWHXBUNZWAWBWCVSWHXBUOZYBYHYBYDXRWTWNWSMLZWRWTWJWPWIUPXRVSW
    AWGWBWCWTYKRXTYBYHYIYJAGBCESUQURXRWOWNWMMLZWLWOWJXAWIUSXRVSWAWFWBWCWOYLRXTY
    BYDYIYJAFBCESUQURUTZVADAGDAFEVBVCYMOQVPWIVSWEWAWFWGWEWAWFWFXFXQRVSWDWHVEZVS
    WDWEWFWGVDZVSWAWBWCWHVFZVSWDWEWFWGVGZVSWDWEWFWGVHZYOYPYQYQDAFGDAFFEVIVJVKXB
    WJTWIWJWPXAVEVLVMWIVSWEWAWFWGWEWAWFWFXGXHTYNYOYPYQYRYOYPYQYQDAFGDAFFEVNVJWI
    VSWFWGWFXHXITYNYQYRYQFGFEVOVQVR $.
    $( [12-Jun-2013] $)

  ${
    $d N r s $.  $d A r s $.  $d B r s $.  $d C r s $.  $d D r s $.
    $( Existential uniqueness version of ~ segconeq .  (Contributed by Scott
       Fenton, 19-Oct-2013.) $)
    segconeu $p |- ( ( N e. NN /\
       ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) /\
         C =/= D ) ) ->
       E! r e. ( EE ` N ) ( D Btwn <. C , r >. /\
       <. D , r >. Cgr <. A , B >. ) ) $=
      ( vs cn wcel cee wa w3a cv cop cbtwn wbr ccgr wi wral opeq2 cfv wrex wreu
      wne simpl simpr2 simpr1 axsegcon syl3anc simpl23 simprl simprr 3jca simp1
      weq simp22r simp21l simp21r simp22l simp3l simp3r segconeq syl133anc syld
      ex 3expa ralrimivva breq2d breq1d anbi12d reu4 sylanbrc ) EHIZAEJUAZIZBVN
      IZKZCVNIZDVNIZKZCDUDZLZKZDCFMZNZOPZDWDNZABNZQPZKZFVNUBZWJDCGMZNZOPZDWLNZW
      HQPZKZKZFGUOZRZGVNSFVNSWJFVNUCWCVMVTVQWKVMWBUEVMVQVTWAUFVMVQVTWAUGFCDABEU
      HUIWCWTFGVNVNVMWBWDVNIZWLVNIZKZWTVMWBXCLZWRWAWJWQLZWSXDWRXEXDWRKWAWJWQVQV
      TWAVMXCWRUJXDWJWQUKXDWJWQULUMVEXDVMVSVOVPVRXAXBXEWSRVMWBXCUNVRVSVQWAVMXCU
      PVOVPVTWAVMXCUQVOVPVTWAVMXCURVRVSVQWAVMXCUSVMWBXAXBUTVMWBXAXBVADABCEWDWLV
      BVCVDVFVGWJWQFGVNWSWFWNWIWPWSWEWMDOWDWLCTVHWSWGWOWHQWDWLDTVIVJVKVL $.
      $( [19-Apr-2014] $) $( [19-Oct-2013] $)
  $}

