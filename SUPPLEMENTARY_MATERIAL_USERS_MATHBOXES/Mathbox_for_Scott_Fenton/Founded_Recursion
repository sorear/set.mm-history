$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Founded Recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A w y z $.  $d A x $.  $d F w y z $.  $d G w y z $.  $d H w y z $.
    $d R w y z $.  $d R x $.  $d x z $.
    $( Functions defined by founded recursion are identical up to relation,
       domain, and characteristic function.  General version of frr3
       (Contributed by Scott Fenton, 10-Feb-2011.) $)
    frr3g $p |- ( ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                     ( F Fn A /\
                       A. y e. A ( F ` y ) =
                                 ( y H ( F |` Pred ( R , A , y ) ) ) ) /\
                     ( G Fn A /\
                       A. y e. A ( G ` y ) =
                                 ( y H ( G |` Pred ( R , A , y ) ) ) ) )
                   -> F = G ) $=
      ( vz vw cv cpred wral wa cfv cres co wceq wi fveq2 syl wfr cvv wcel ax-17
      wfn w3a ra5 r19.26 anbi2i predeq3 reseq2d oveq12d eqeq12d anbi12d rcla4va
      weq id wss wb predss fvreseq mpan2 biimpar oveq2d eqcomd eqtr3 ex expimpd
      com12 exp3a com23 imp3a syl5bir a2d syl5 imbi2d frins2g com3r an4s 3impib
      ra4 ralrimiv eqid jctil eqfnfv ad2ant2r 3adant1 mpbird ) CDUACDAJKUBUCACL
      MZECUEZBJZENZWKECDWKKZOZGPZQZBCLZMZFCUEZWKFNZWKFWMOZGPZQZBCLZMZUFZEFQZCCQ
      ZHJZENZXIFNZQZHCLZMZXFXMXHXFXLHCWIWRXEXICUCZXLRZWRXEMWIXPWJWSWQXDWIXPRWIX
      OWJWSMZWQXDMZMZXLWIXSXLRZHCLXOXTRXTXSIJZENZYAFNZQZRZAHICDYEICDXIKZLXSYDIY
      FLZRXOXTXSYDIYFXSIUDUGXOXSYGXLXSXQWPXCMZBCLZMXOYGXLRZYIXRXQWPXCBCUHUIXOXQ
      YIYJXOYIXQYJXOYIXQYJRZXOYIMXJXIEYFOZGPZQZXKXIFYFOZGPZQZMZYKYHYRBXICBHUPZW
      PYNXCYQYSWLXJWOYMWKXIESYSWKXIWNYLGYSUQZYSWMYFECDWKXIUJZUKULUMYSWTXKXBYPWK
      XIFSYSWKXIXAYOGYTYSWMYFFUUAUKULUMUNUOYRXQYGXLXQYGMZYRXLUUBYPYMQZYRXLRUUBY
      MYPUUBYLYOXIGXQYLYOQZYGXQYFCURUUDYGUSCDXIUTICYFEFVAVBVCVDVEUUCYNYQXLUUCYN
      MZXJYPQZYQXLRUUEYPXJYPXJYMVFVEUUFYQXLXJXKYPVFVGTVHTVIVJTVGVKVLVMVNVOHIUPZ
      XLYDXSUUGXJYBXKYCXIYAESXIYAFSUMVPVQXTHCWATVRVSVIVTWBCWCWDWRXEXGXNUSZWIWJW
      SUUHWQXDHCCEFWEWFWGWH $.
      $( [10-Feb-2011] $)
  $}

  ${
    $d A f g w x y z $.  $d f G g w x y z $.  $d R f g w x y z $.
    frrlem1.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x /\
                                  A. y e. x ( f ` y ) =
                                            ( y G ( f |` Pred ( R , A , y ) ) )
                                 ) ) } $.
    $( Lemma for founded recursion.  The final item we are interested in is the
       union of acceptable functions ` B ` .  This lemma just changes bound
       variables for later use. $)
    frrlem1 $p |- B = { g |
                         E. z ( g Fn z /\
                                ( z C_ A /\
                                  A. w e. z Pred ( R , A , w ) C_ z /\
                                  A. w e. z ( g ` w ) =
                                            ( w G ( g |` Pred ( R , A , w ) ) )
                        ) ) } $=
      ( cv wfn wss wral cfv cres co wceq w3a cpred wa wex cab weq reseq1 oveq2d
      fneq1 fveq1 eqeq12d ralbidv 3anbi3d anbi12d exbidv fneq2 sseq1 raleqbi1dv
      sseq2 predeq3 sseq1d cbvralv syl6bb raleq fveq2 reseq2d oveq12d 3anbi123d
      id cbvexv cbvabv eqtri ) FHLZALZMZVMENZEGBLZUAZVMNZBVMOZVPVLPZVPVLVQQZJRZ
      SZBVMOZTZUBZAUCZHUDILZCLZMZWIENZEGDLZUAZWINZDWIOZWLWHPZWLWHWMQZJRZSZDWIOZ
      TZUBZCUCZIUDKWGXCHIHIUEZWGWHVMMZVOVSVPWHPZVPWHVQQZJRZSZBVMOZTZUBZAUCXCXDW
      FXLAXDVNXEWEXKVMVLWHUHXDWDXJVOVSXDWCXIBVMXDVTXFWBXHVPVLWHUIXDWAXGVPJVLWHV
      QUFUGUJUKULUMUNXLXBACACUEZXEWJXKXAVMWIWHUOXMVOWKVSWOXJWTVMWIEUPXMVSVQWINZ
      BWIOWOVRXNBVMWIVMWIVQURUQXNWNBDWIBDUEZVQWMWIEGVPWLUSZUTVAVBXMXJXIBWIOWTXI
      BVMWIVCXIWSBDWIXOXFWPXHWRVPWLWHVDXOVPWLXGWQJXOVHXOVQWMWHXPVEVFUJVAVBVGUMV
      IVBVJVK $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)

    $( Lemma for founded recursion.  An acceptable function is a function. $)
    frrlem2 $p |- ( g e. B -> Fun g ) $=
      ( vz vw cv wcel wfn wss cpred wral cfv cres co wceq w3a wa frrlem1 abeq2i
      wex wfun fnfun adantr exlimiv sylbi ) GLZDMULJLZNZUMCOCEKLZPZUMOKUMQUOULR
      UOULUPSHTUAKUMQUBZUCZJUFZULUGZUSGDABJKCDEFGHIUDUEURUTJUNUTUQUMULUHUIUJUK
      $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)

    $( Lemma for founded recursion.  An acceptable function's domain is a
       subset of ` A ` . $)
    frrlem3 $p |- ( g e. B -> dom g C_ A ) $=
      ( vz vw cv wcel wfn wss cpred wral cfv cres wceq co w3a wa wex cdm abeq2i
      frrlem1 fndm simp1 sseq1 biimpar syl2an exlimiv sylbi ) GLZDMUOJLZNZUPCOZ
      CEKLZPZUPOKUPQZUSUORUSUOUTSHUATKUPQZUBZUCZJUDZUOUEZCOZVEGDABJKCDEFGHIUGUF
      VDVGJUQVFUPTZURVGVCUPUOUHURVAVBUIVHVGURVFUPCUJUKULUMUN $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)
  $}

  ${
    $d A a f g $.  $d A a h x y $.  $d B a $.  $d f h x y $.  $d G a f g $.
    $d G h $.  $d G g x y $.  $d R a f g $.  $d R h x y $.  $d A a f g b c $.
    $d h x y b c $.  $d G b c $.  $d R b c $.
    frrlem4.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x /\
                                  A. y e. x ( f ` y ) =
                                            ( y G ( f |` Pred ( R , A , y ) ) )
                                 ) ) } $.
    $( Lemma for founded recursion.  Properties of the restriction of an
       acceptable function to the domain of another acceptable function. $)
    frrlem4 $p |- ( ( g e. B /\ h e. B ) ->
       ( ( g |` ( dom g i^i dom h ) ) Fn ( dom g i^i dom h ) /\
         A. a e. ( dom g i^i dom h ) ( ( g |` ( dom g i^i dom h ) ) ` a ) =
            ( a G ( ( g |` ( dom g i^i dom h ) ) |`
                    Pred ( R , ( dom g i^i dom h ) , a ) ) ) ) ) $=
      ( vb vc cv wcel wa cres wceq wral wss cdm cin wfn cfv cpred co wfun funfn
      frrlem2 sylib fnresin1 syl adantr inss1 sseli w3a wex frrlem1 abeq2i fndm
      wel ra4 3ad2ant3 eleq2 imbi1d syl5ibrcom mpan9 exlimiv sylbi syl5 adantlr
      wi imp fvres adantl resres predss sseqin2 mpbi eeanv simpl1 simpl2 simpr2
      ssinss1 hbra1 hban syl5com anim12d ssin biimpi syl6com ralrimi syl2anc wb
      inss2 ineq12 sseq1d sseq2 raleqbi1dv syl2an biimprcd impcom an4s exlimivv
      anbi12d sylbir syl2anb preddowncl syl5eq reseq2d oveq2d 3eqtr4d ralrimiva
      simpr sylc jca ) GNZDOZHNZDOZPZXQXQUAZXSUAZUBZQZYDUCZJNZYEUDZYGYEYDEYGUEZ
      QZIUFZRZJYDSXRYFXTXRXQYBUCZYFXRXQUGYMABCDEFGIKUIXQUHUJYBYCXQUKULUMYAYLJYD
      YAYGYDOZPZYGXQUDZYGXQCEYGUEZQZIUFZYHYKXRYNYPYSRZXTXRYNYTYNYGYBOZXRYTYDYBY
      GYBYCUNUOXRXQLNZUCZUUBCTZYQUUBTZJUUBSZYTJUUBSZUPZPZLUQZUUAYTVLZUUJGDABLJC
      DEFGIKURUSZUUIUUKLUUCYBUUBRZUUHUUKUUBXQUTZUUHUUKUUMJLVAZYTVLZUUGUUDUUPUUF
      YTJUUBVBVCUUMUUAUUOYTYBUUBYGVDVEVFVGVHVIVJVMVKYNYHYPRYAYGYDXQVNVOYOYJYRYG
      IYOYJXQYDYIUBZQYRXQYDYIVPYOUUQYQXQYOUUQYIYQYIYDTUUQYIRYDEYGVQYIYDVRVSYOYD
      CTZYQYDTZJYDSZPZYNYIYQRYAUVAYNXRUUJXSMNZUCZUVBCTZYQUVBTZJUVBSZYGXSUDYGXSY
      QQIUFRJUVBSZUPZPZMUQZUVAXTUULUVJHDABMJCDEFHIKURUSUUJUVJPUUIUVIPZMUQLUQUVA
      UUIUVILMVTUVKUVALMUUCUVCUUHUVHUVAUUHUVHPZUUCUVCPZUVAUVLUUBUVBUBZCTZYQUVNT
      ZJUVNSZUVMUVAVLUVLUUDUVOUUDUUFUUGUVHWAUUBUVBCWDULUVLUUFUVFUVQUUDUUFUUGUVH
      WBUUHUVDUVFUVGWCUUFUVFPZUVPJUVNUUFUVFJUUEJUUBWEUVEJUVBWEWFYGUVNOZUVRUUEUV
      EPZUVPUVSUUFUUEUVFUVEUVSUUOUUFUUEUVNUUBYGUUBUVBUNUOUUEJUUBVBWGUVSJMVAUVFU
      VEUVNUVBYGUUBUVBWOUOUVEJUVBVBWGWHUVTUVPYQUUBUVBWIWJWKWLWMUVMUVAUVOUVQPZUU
      CUUMYCUVBRZUVAUWAWNUVCUUNUVBXSUTUUMUWBPZUURUVOUUTUVQUWCYDUVNCYBUUBYCUVBWP
      ZWQUWCYDUVNRUUTUVQWNUWDUUSUVPJYDUVNYDUVNYQWRWSULXEWTXAWMXBXCXDXFXGUMYAYNX
      NJCYDEYGXHXOXIXJXIXKXLXMXP $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)
  $}

  ${
    $d A f g $.  $d A h x y $.  $d f g $.  $d f h x y G $.  $d g G $.
    $d h G $.  $d u g v x $.  $d x G $.  $d y g G $.  $d u h v x $.  $d y h $.
    $d R f g $.  $d R h x y $.  $d v u x $.  $d A z $.  $d B z $.  $d C z $.
    $d f z $.  $d G z $.  $d R z $.  $d x z $.  $d y z $.  $d C g $.
    $d B g h u v $.  $d B x $.  $d A a $.  $d f h x y a $.  $d g a $.
    $d B a $.  $d G a $.  $d R a $.
    frrlem5.1 $e |- R Fr A $.
    frrlem5.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frrlem5.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x /\
                                  A. y e. x ( f ` y ) =
                                            ( y G ( f |` Pred ( R , A , y ) ) )
                                 ) ) } $.
    $( Lemma for founded recursion.  The values of two acceptable functions
       agree within their domains. $)
    frrlem5 $p |- ( ( g e. B /\ h e. B ) -> ( ( x g u /\ x h v )
             -> u = v ) ) $=
      ( va cv wa cres wcel wceq wbr cdm cin weq vex breldm anim12i elin anandir
      sylibr brres anbi12i bitr4i biimpi mpdan wfr cpred cvv wfn cfv co frrlem3
      wral wss ssinss1 frss mpi setlikess mpan2 3syl adantr frrlem4 ancom incom
      reseq2i fneq1i fneq2i bitri eleq2i fveq1i predeq2 reseq12i oveq2i eqeq12i
      ax-mp imbi12i ralbii2 3imtr4i frr3g syl3anc breqd biimprd wi wfun frrlem2
      jca funres wal wrel dffun2 simprbi ax-4 a4s syl sylan2d syl5 ) APZDPZIPZU
      AZXGCPZJPZUAZQZXGXHXIXIUBZXLUBZUCZRZUAZXGXKXLXQRZUAZQZXIFSZXLFSZQZDCUDZXN
      XGXQSZYBXNXGXOSZXGXPSZQYGXJYHXMYIXGXHXIAUEZUFXGXKXLYJUFUGXGXOXPUHUJXNYGQZ
      YBYKXJYGQZXMYGQZQYBXJXMYGUIXSYLYAYMXGXHXIXQDUEUKXGXKXLXQCUEUKULUMUNUOYEYA
      XGXKXRUAZXSYFYEYNYAYEXRXTXGXKYEXQGUPZXQGXGUQURSAXQVCZQZXRXQUSOPZXRUTYRXRX
      QGYRUQZRKVATOXQVCQXTXQUSZYRXTUTZYRXTYSRZKVAZTZOXQVCZQZXRXTTYCYQYDYCXOEVDX
      QEVDZYQABEFGHIKNVBXOXPEVEUUGYOYPUUGEGUPYOLXQEGVFVGUUGEGXGUQURSAEVCYPMAXQE
      GVHVIWPVJVKABEFGHIJKONVLYDYCQXLXPXOUCZRZUUHUSZYRUUIUTZYRUUIUUHGYRUQZRZKVA
      ZTZOUUHVCZQYEUUFABEFGHJIKONVLYCYDVMYTUUJUUEUUPYTUUIXQUSUUJXQXTUUIXQUUHXLX
      OXPVNZVOZVPXQUUHUUIUUQVQVRUUDUUOOXQUUHYRXQSYRUUHSUUDUUOXQUUHYRUUQVSUUAUUK
      UUCUUNYRXTUUIUURVTUUBUUMYRKXTUUIYSUULUURXQUUHTYSUULTUUQXQUUHGYRWAWEWBWCWD
      WFWGULWHAOXQGXRXTKWIWJWKWLYCXSYNQYFWMZYDYCXIWNXRWNZUUSABEFGHIKNWOXQXIWQUU
      TUUSCWRZDWRZAWRZUUSUUTXRWSUVCADCXRWTXAUVBUUSAUVAUUSDUUSCXBXCXCXDVJVKXEXF
      $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)

    $( Lemma for founded recursion.  The union of a subclass of ` B ` is a
       relationship. $)
    frrlem5b $p |- ( C C_ B -> Rel U. C ) $=
      ( vz wss cv wrel wral cuni wcel ssel wfun funrel syl syl6 ralrimiv reluni
      frrlem2 sylibr ) EDMZLNZOZLEPEQOUHUJLEUHUIERUIDRZUJEDUISUKUITUJABCDFGLHKU
      FUIUAUBUCUDLEUEUG $.
      $( [12-May-2012] $) $( [29-Apr-2012] $)

    $( Lemma for founded recursion.  The union of a subclass of ` B ` is a
       function. $)
    frrlem5c $p |- ( C C_ B -> Fun U. C ) $=
      ( vu vv vg vh cv wbr wa wcel wex wss cuni wfun wrel wceq wi ssid frrlem5b
      wal ax-mp cop eluni df-br anbi1i exbii 3bitr4i anbi12i eeanv frrlem5 an4s
      bitr4i impcom exlimivv sylbi ax-gen gen2 dffun2 mpbir2an uniss funss syl
      mpi ) EDUAZDUBZUCZEUBZUCZVOVNUDZAPZLPZVNQZVSMPZVNQZRZVTWBUEZUFZMUIZLUIAUI
      DDUAVRDUGABCDDFGHIJKUHUJWGALWFMWDVSVTNPZQZWHDSZRZVSWBOPZQZWLDSZRZRZOTNTZW
      EWDWKNTZWOOTZRWQWAWRWCWSVSVTUKZVNSWTWHSZWJRZNTWAWRNWTDULVSVTVNUMWKXBNWIXA
      WJVSVTWHUMUNUOUPVSWBUKZVNSXCWLSZWNRZOTWCWSOXCDULVSWBVNUMWOXEOWMXDWNVSWBWL
      UMUNUOUPUQWKWONOURVAWPWENOWIWMWJWNWEWJWNRWIWMRWEABMLCDFGNOHIJKUSVBUTVCVDV
      EVFALMVNVGVHVMVPVNUAVOVQUFEDVIVPVNVJVKVL $.
      $( [12-May-2012] $) $( [29-Apr-2012] $)

    $( Lemma for founded recursion.  The domain of the union of a subset of
       ` B ` is a subset of ` A ` . $)
    frrlem5d $p |- ( C C_ B -> dom U. C C_ A ) $=
      ( vg wss cuni cdm cv ciun dmuni wral wcel ssel syl6 ralrimiv iunss sylibr
      frrlem3 syl5eqss ) EDMZENOLELPZOZQZCLERUHUJCMZLESUKCMUHULLEUHUIETUIDTULED
      UIUAABCDFGLHKUFUBUCLEUJCUDUEUG $.
      $( [12-May-2012] $) $( [29-Apr-2012] $)

    ${
      $d A t w z $.  $d f t w z $.  $d G t w z $.  $d R t w z $.
      $d x y t w z $.  $d X t w z $.  $d C w $.
      $( Lemma for founded recursion.  The domain of the union of a subset of
         ` B ` is closed under predecessors. $)
      frrlem5e $p |- ( C C_ B -> ( X e. dom U. C ->
                    Pred ( R , A , X ) C_ dom U. C ) ) $=
        ( vz vw vt cdm wcel wss wi wceq cuni wrex cpred ciun dmuni eleq2i eliun
        cv bitri wa ssel2 wfn wral cfv cres w3a wex frrlem1 abeq2i fndm predeq3
        co sseq1d rcla4cv 3ad2ant2 eleq2 sseq2 imbi12d syl5ibr syl exlimiv dmeq
        imp sylbi sseq2d rcla4ev ssiun syl6sseqr adantl syld rexlimdva syl5bi
        ex ) IEUAPZQZIMUHZPZQZMEUBZEDRZCFIUCZWDRZWEIMEWGUDZQWIWDWMIMEUEUFMIEWGU
        GUIWJWHWLMEWJWFEQZUJZWHWKWGRZWLWOWFDQZWHWPSZEDWFUKWQWFNUHZULZWSCRZCFOUH
        ZUCZWSRZOWSUMZXBWFUNXBWFXCUOHVBTOWSUMZUPZUJZNUQZWRXIMDABNOCDFGMHLURUSXH
        WRNWTXGWRWTWGWSTZXGWRSWSWFUTXGWRXJIWSQZWKWSRZSZXEXAXMXFXDXLOIWSXBITXCWK
        WSCFXBIVAVCVDVEXJWHXKWPXLWGWSIVFWGWSWKVGVHVIVJVMVKVNVJWNWPWLSWJWNWPWLWN
        WPUJZWKNEWSPZUDZWDXNWKXORZNEUBWKXPRXQWPNWFEWSWFTXOWGWKWSWFVLVOVPNEXOWKV
        QVJNEUEVRWCVSVTWAWB $.
        $( [12-May-2012] $) $( [1-May-2012] $)
    $}

    $(
    @{ @d B f @. @d C f @. @d C x @.
    @( Lemma for founded recursion. Calculate the value of the union
       of a subset of ` B ` within its domain. @)
    frrlem5f @p |- ( C C_ B -> ( y e. dom U. C ->
       ( U. C ` y ) = ( y G ( U. C |` Pred ( R , A , y ) ) ) ) ) @=
      ( vz wss cv cop cuni wcel wex cfv cpred cres co wceq cdm wrex wi ssel
      wfn wral w3a wa imp3a exlimdv abeq2i syl5ibOLD syld rexlimdv eluni2
      vex eldm2 ) EDMZBNZLNOZEPZQZLRVBVDSVBVDCFVBTZUAHUBUCZVBVDUDQVAVEVGLVAV
      CGNZQZGEUEVGVEVAVIVGGEVAVHEQVHDQZVIVGUFZEDVHUGVAVHANZUHZVLCMVFVLMBVLUIVBV
      HSVBVHVFUAHUBUCBVLUIUJZUKZARZVKVJVAVOVKAVAVMVNVK?ULUMVPGDKUNUOUPUQGVCEURU
      OUMLVBVDBUSUTUO @.
      @( [12-May-2012] @) @( [2-May-2012] @)
    @}

    @{ @d A w @. @d f w @. @d g w @. @d G w @. @d g z @. @d H g @. @d H h @.
       @d H w @. @d H z @. @d R w @. @d w x @. @d w y @. @d w z @. @d X g @.
       @d X h @. @d X w @. @d X x @. @d X z @.
    @( Lemma for founded recursion. Extend the domain of a particular
       acceptable function by one. @)
    frrlem5g @p |- ( ( X e. A /\ H e. B /\ H Fn TrPred ( R , A , X ) ) ->
                     E. h e. B h Fn ( { X } u. TrPred ( R , A , X ) ) ) @=
      ( vz vw vg wcel ctrpred wfn w3a cres co cop csn cun cv wrex wss cpred
      wral cfv wceq wa wex wfun cdm cin c0 cvv ovex funsngOLD mpan2 3ad2ant1
      frrlem2 3ad2ant2 jca fndm ineq2d 3ad2ant3 wn wal wi eleq1 notbid
      trpredfrirr syl5cbirOLD elsn syl5ibOLD elin notbii pm4.63 con1bii bitri
      sylibr
      alrimiv eq0 eqtrd dmsnop ineq1i syl5eqOLD funun syl uneq2d dmun uneq1i
      eqtri df-fn snssi setlikespec trpredss unss sylib wo predeq3 sseq1d
      trpredpred trpredtr jaod imp elun orbi1i sylan2bOLD ssun4 ralrimiva 3jca
      snex trpredex unex fneq2 sseq1 sseq2 raleqbi1dv raleq 3anbi123d anbi12d
      cla4ev wb unexg mpan fneq1 fveq1 reseq1 oveq2d eqeq12d ralbidv 3anbi3d
      exbidv frrlem1 elab2g mpbird jctir simp3 fnun syl21anc rcla4ev syl2anc
      ) JCQZIDQZICEJRZSZTZJJIUUIUAZHUBZUCZUDZIUEZDQZUUPJUDZUUIUEZSZGUFZUUSSZGDU
      GUUKUUQUUPNUFZSZUVCCUHZCEOUFZUIZUVCUHZOUVCUJZUVFUUPUKZUVFUUPUVGUAZHUBZULZ
      OUVCUJZTZUMZNUNZUUKUUTUUSCUHZUVGUUSUHZOUUSUJZUVMOUUSUJZTZUMZUVQUUKUUTUWBU
      UKUUPUOZUUPUPZUUSULZUMUUTUUKUWDUWFUUKUUOUOZIUOZUMZUUOUPZIUPZUQZURULZUMUWD
      UUKUWIUWMUUKUWGUWHUUGUUHUWGUUJUUGUUMUSQUWGJUULHUTJUUMCUSVAVBVCZUUHUUGUWHU
      UJABCDEF?HMVDVEVFUUKUURUWKUQZURUWLUUKUWOUURUUIUQZURUUJUUGUWOUWPULUUHUUJUW
      KUUIUURUUIIVGZVHVIUUKUVCUWPQZVJZNVKZUWPURULZUUKUWSNUUKUVCUURQZUVCUUIQZVJZ
      VLZUWSUUKUVCJULZUXDUXBUXFUXDJUUIQZVJZUUKUXFUXCUXGUVCJUUIVMVNZUUGUUHUXHUUJ
      ACEJKLVOZVCVPNJVQZVRUWSUXBUXCUMZVJUXEUWRUXLUVCUURUUIVSVTUXEUXLUXBUXCWAWBW
      CZWDWENUWPWFZWDWGUWJUURUWKJUUMWHZWIWJVFUUOIWKWLUUJUUGUWFUUHUUJUURUWKUEZUU
      SUWEUUJUWKUUIUURUWQWMUWEUWJUWKUEUXPUUOIWNUWJUURUWKUXOWOWPWJVIVFUUPUUSWQWD
      UUKUVRUVTUWAUUGUUHUVRUUJUUGUURCUHZUUICUHZUMUVRUUGUXQUXRJCWRUUGCEJUIZUSQZU
      XRUUGCEAUFUIUSQACUJZUXTLACEJWSZVBCUSEJWTWLVFUURUUICXAXBVCUUGUUHUVTUUJUUGU
      VSOUUSUUGUVFUUSQZUMUVGUUIUHZUVSUUGUVFJULZUVFUUIQZXCZUYDUYCUUGUYGUYDUUGUYA
      UYGUYDVLLUUGUYAUMZUYEUYDUYFUYEUYDUXSUUIUHZUYHUYEUVGUXSUUICEUVFJXDXEUYHUXT
      UYIUYBCUSEJXFWLVPACEJUVFXGXHVBXIUYCUVFUURQZUYFXCUYGUVFUURUUIXJUYJUYEUYFOJ
      VQXKWCXLUVGUUIUURXMWLXNVC?XOVFUVPUWCNUUSUURUUIJXPCEJXQXRUVCUUSULZUVDUUTUV
      OUWBUVCUUSUUPXSUYKUVEUVRUVIUVTUVNUWAUVCUUSCXTUVHUVSOUVCUUSUVCUUSUVGYAYBUV
      MOUVCUUSYCYDYEYFWLUUKUUPUSQZUUQUVQYGUUHUUGUYLUUJUUOUSQUUHUYLUUNXPUUOIUSDY
      HYIVEPUFZUVCSZUVEUVIUVFUYMUKZUVFUYMUVGUAZHUBZULZOUVCUJZTZUMZNUNUVQPUUPDUS
      UYMUUPULZVUAUVPNVUBUYNUVDUYTUVOUVCUYMUUPYJVUBUYSUVNUVEUVIVUBUYRUVMOUVCVUB
      UYOUVJUYQUVLUVFUYMUUPYKVUBUYPUVKUVFHUYMUUPUVGYLYMYNYOYPYEYQABNOCDEFPHMYRY
      SWLYTUUKUUOUURSZUUJUXAUUTUUKUWGUWJUURULZUMVUCUUKUWGVUDUWNUXOUUAUUOUURWQWD
      UUGUUHUUJUUBUUGUUHUXAUUJUUGUWTUXAUUGUWSNUUGUXEUWSUUGUXFUXDUXBUXFUXDUXHUUG
      UXIUXJVPUXKVRUXMWDWEUXNWDVCUURUUIUUOIUUCUUDUVBUUTGUUPDUUSUVAUUPYJUUEUUF
      @.
      @( [12-May-2012] @) @( [5-May-2012] @)
    @}
    $)
  $}

  ${
    $d A f g x y $.  $d G f g x y $.  $d R f g x y $.  $d B g $.  $d A w z $.
    $d f w z $.  $d g w z $.  $d G w z $.  $d R w z $.  $d x w z $.
    $d y w z $.  $d R w z $.  $d X g $.  $d X w z $.
    frrlem6.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x /\
                                  A. y e. x ( f ` y ) =
                                            ( y G ( f |` Pred ( R , A , y ) ) )
                                 ) ) } $.
    frrlem6.2 $e |- F = U. B $.
    $( Lemma for founded recursion.  The union of all acceptable functions is a
       relationship. $)
    frrlem6 $p |- Rel F $=
      ( vg cuni wceq wrel cv reluni wcel wfun frrlem2 funrel syl mprgbir mpbiri
      releq ax-mp ) GDLZMZGNZJUGUHUFNZUIKOZNZKDKDPUJDQUJRUKABCDEFKHISUJTUAUBGUF
      UDUCUE $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)

    $( Lemma for founded recursion.  The domain of ` F ` is a subclass of
       ` A ` . $)
    frrlem7 $p |- dom F C_ A $=
      ( vg cdm cv ciun cuni dmeqi dmuni eqtri wss iunss frrlem3 mprgbir eqsstri
      ) GLZKDKMLZNZCUDDOZLUFGUGJPKDQRUFCSUECSKDKDUECTABCDEFKHIUAUBUC $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)
  $}

  ${
    $d A f x y $.  $d F x $.  $d G f x y $.  $d R f x y $.  $d B x $.
    $d A f x y g h u v $.  $d B g h $.  $d F u v $.  $d G g h $.  $d R g h $.
    frrlem10.1 $e |- R Fr A $.
    frrlem10.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frrlem10.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x /\
                                  A. y e. x ( f ` y ) =
                                            ( y G ( f |` Pred ( R , A , y ) ) )
                                 ) ) } $.
    frrlem10.4 $e |- F = U. B $.
    $( Lemma for founded recursion.  The union of all acceptable functions is a
       function. $)
    frrlem10 $p |- Fun F $=
      ( wfun cuni wss ssid frrlem5c ax-mp funeqi mpbir ) GMDNZMZDDOUBDPABCDDEFH
      IJKQRGUALST $.
      $( [12-May-2012] $) $( [21-Apr-2012] $)
    ${
      $d A z $.  $d F f $.  $d F z $.  $d G z $.  $d R z $.  $d x y z $.
      $d f z $.
      $( Lemma for founded recursion.  Here, we calculate the value of ` F `
         (the union of all acceptable functions). $)
      frrlem11 $p |- ( y e. dom F ->
              ( F ` y ) = ( y G ( F |` Pred ( R , A , y ) ) ) ) $=
        ( vz cv wcel wex wceq wss wa wi cdm cop cfv cpred cres co vex eldm2 wfn
        wral w3a cab unieqi eqtri eleq2i eluniab bitri abeq2i elssuni syl6sseqr
        cuni sylbir wel fnop ex ra4 impcom fndm sseq2 eleq2 anbi12d syl biimprd
        wb exp3a wfun frrlem10 funssfv 3adant3l fun2ssres oveq2d eqeq12d mp3an1
        3adant3r expcom com23 syl6com com34 sylcom com14 syl7 exp4a pm2.43d imp
        adantl 3impd syld com12 imp3a exlimdv mpdi exlimiv sylbi ) BNZGUAOXDMNZ
        UBZGOZMPXDGUCZXDGCEXDUDZUEZHUFZQZMXDGBUGUHXGXLMXGXFFNZOZXMANZUIZXOCRZXI
        XORZBXOUJZXDXMUCZXDXMXIUEZHUFZQZBXOUJZUKZSZAPZSZFPZXLXGXFYGFULZVAZOYIGY
        KXFGDVAZYKLDYJKUMUNUOYGFXFUPUQYHXLFXNYGXLXNYGXMGRZXLYGXMDOZYMYGFDKURYNX
        MYLGXMDUSLUTVBXNYFYMXLTZAXNXPYEYOXPXNYEYOTZXPXNBAVCZYPXPXNYQXOXDXEXMVDV
        EXPYQYPXPYQSZXQXSYDYOYRXQXSYDYOTZXPYQXQXSSZYSTXPYQYDYTYOXPYQYDYTYOTZTXP
        YQYQYDUUAYQYDSYCXPYQUUAYDYQYCYCBXOVFVGYTYQYCXPYOXSYQYCXPYOTTZTXQXSYQXRU
        UBXRBXOVFYQXRXPYCYOYQXRXPYCYOTZXRXPSYQXIXMUAZRZXDUUDOZSZUUCXPXRYQUUGTXP
        XRYQUUGXPUUGXRYQSZXPUUDXOQZUUGUUHVNXOXMVHUUIUUEXRUUFYQUUDXOXIVIUUDXOXDV
        JVKVLVMVOVGUUGYMYCXLYMUUGYCXLTZGVPZYMUUGUUJABCDEFGHIJKLVQUUKYMUUGUKZXLY
        CUULXHXTXKYBUUKYMUUFXHXTQUUEXDGXMVRVSUULXJYAXDHUUKYMUUEXJYAQUUFXIGXMVTW
        DWAWBVMWCWEWFWGVOWHWIWOWJWKWLWMWHWNVOWPVEWQWRWSWTXAWNXBXCXBXC $.
        $( [12-May-2012] $) $( [21-Apr-2012] $)
    $}

    $(
    @{ @d A s t w z @. @d B s t w z @. @d R s t w z @. @d F z @.
       @d g w @. @d g z @. @d h w @. @d h z @.
    @( Calculate the domain of ` F ` . @)
    frrlem12 @p |- dom F = A @=
      ( vz vg vh vw cdm frrlem7 cv wcel ciun wrex csn ctrpred cun wfn wceq
      sneq trpredeq3 uneq12d fneq2d rexbidv fneq1
      cbvrexv syl5bbOLD frins2 vex
      snid elun1 ax-mp fndm eleq2d mpbiri reximi syl eliun sylibr cuni dmeqi
      dmuni eqtri eqcomi syl6eleq ssriv eqssi ) GQZCABCDEFGHKLRMCVPMSZCTZVQNDNS
      ZQZUAZVPVRVQVTTZNDUBZVQWATVRVSVQUCZCEVQUDZUEZUFZNDUBZWCWHOSZPSZUCZCEWJUDZ
      UEZUFZODUBZAMPCEIJVQWJUGZWIWFUFZODUBWOWHWPWQWNODWPWFWMWIWPWDWKWEWLVQWJUHC
      EVQWJUIUJUKULWGWQNODWFVSWIUMUNUO?UPWGWBNDWGWBVQWFTZVQWDTWRVQMUQURVQWDWEUS
      UTWGVTWFVQWFVSVAVBVCVDVENVQDVTVFVGVPWAVPDVHZQWAGWSLVINDVJVKVLVMVNVO @.
      @( [12-May-2012] @) @( [28-Apr-2012] @)
   @}
   $)
  $}


