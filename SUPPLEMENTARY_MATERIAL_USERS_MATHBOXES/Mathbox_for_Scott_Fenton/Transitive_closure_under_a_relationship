$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Transitive closure under a relationship
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TrPred $.
  $( Define the transitive predecessor class as a class. $)
  ctrpred $a class TrPred ( R , A , X ) $.

  ${
    $d R a y $.  $d A a y $.  $d X a y $.
    $( Define the transitive predecessors of a class ` X ` under a relationship
       ` R ` and a class ` A ` .  This class can be thought of as the
       "smallest" class containing all elements of ` A ` that are linked to
       ` X ` by a chain of ` R ` relationships (see ~ trpredtr and
       ~ trpredmintr ).  Definition based off of Lemma 4.2 of Don Monk's notes
       for Advanced Set Theory, which can be found at
       ~ http://euclid.colorado.edu/~~monkd/settheory (check The Internet
       Archive for it now as Prof.  Monk appears to have rewritten his
       website). $)
    df-trpred $a |- TrPred ( R , A , X ) =
                        U. ran ( rec ( ( a e. _V |->
                                         U_ y e. a Pred ( R , A , y ) ) ,
                                       Pred ( R , A , X ) ) |` om ) $.
  $}

  ${
    $d R a i y $.  $d A a i y $.  $d X a i y $.
    $( A definition of the transitive predecessors of a class in terms of
       indexed union.  (Contributed by Scott Fenton, 28-Apr-2012.) $)
    dftrpred2 $p |- TrPred ( R , A , X ) =
        U_ i e. om ( ( rec ( ( a e. _V |-> U_ y e. a Pred ( R , A , y ) ) ,
                             Pred ( R , A , X ) ) |` om ) ` i ) $=
      ( ctrpred cvv cv cpred ciun cmpt crdg com cres crn cuni cfv df-trpred wfn
      wceq frfnom fniunfv ax-mp eqtr4i ) BCEGFHAFIBCAIJKLZBCEJZMNOZPQZDNDIUHRKZ
      ABCEFSUHNTUJUIUAUGUFUBDNUHUCUDUE $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)
  $}

  ${
    $d R a y $.  $d S a y $.  $d A a y $.  $d B a y $.  $d X a y $.
    $d Y a y $.

    $( Equality theorem for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq1 $p |- ( R = S -> TrPred ( R , A , X ) = TrPred ( S , A , X ) ) $=
      ( va vy wceq cvv cpred ciun cmpt crdg com cres crn cuni ctrpred df-trpred
      cv predeq1 adantr iuneq2dv mpteq2dv rdgeq12 syl2anc reseq1d rneqd 3eqtr4g
      wel unieqd ) BCGZEHFESZABFSZIZJZKZABDIZLZMNZOZPEHFULACUMIZJZKZACDIZLZMNZO
      ZPABDQACDQUKUTVGUKUSVFUKURVEMUKUPVCGUQVDGURVEGUKEHUOVBUKFULUNVAUKUNVAGFEU
      IABCUMTUAUBUCABCDTUQVDUPVCUDUEUFUGUJFABDERFACDERUH $.
      $( [2-Feb-2011] $)

    $( Equality theorem for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq2 $p |- ( A = B -> TrPred ( R , A , X ) = TrPred ( R , B , X ) ) $=
      ( va vy wceq cvv cpred ciun cmpt crdg com cres crn cuni ctrpred df-trpred
      cv predeq2 wcel adantr iuneq2dv mpteq2dv wa rdgeq12 reseq1d syl2anc rneqd
      unieqd 3eqtr4g ) ABGZEHFESZACFSZIZJZKZACDIZLZMNZOZPEHFUMBCUNIZJZKZBCDIZLZ
      MNZOZPACDQBCDQULVAVHULUTVGULUQVDGZURVEGZUTVGGULEHUPVCULFUMUOVBULUOVBGUNUM
      UAABCUNTUBUCUDABCDTVIVJUEUSVFMURVEUQVDUFUGUHUIUJFACDERFBCDERUK $.
      $( [2-Feb-2011] $)

    $( Equality theorem for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq3 $p |- ( X = Y -> TrPred ( R , A , X ) = TrPred ( R , A , Y ) ) $=
      ( va vy wceq cvv cpred ciun cmpt crdg com cres crn cuni ctrpred df-trpred
      cv predeq3 rdgeq2 reseq1 3syl rneqd unieqd 3eqtr4g ) CDGZEHFESABFSIJKZABC
      IZLZMNZOZPUHABDIZLZMNZOZPABCQABDQUGULUPUGUKUOUGUIUMGUJUNGUKUOGABCDTUIUMUH
      UAUJUNMUBUCUDUEFABCERFABDERUF $.
      $( [2-Feb-2011] $)
  $}

  ${
    trpredeq1d.1 $e |- ( ph -> R = S ) $.
    $( Equality deduction for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq1d $p |- ( ph -> TrPred ( R , A , X ) = TrPred ( S , A , X ) ) $=
      ( wceq ctrpred trpredeq1 syl ) ACDGBCEHBDEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    trpredeq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq2d $p |- ( ph -> TrPred ( R , A , X ) = TrPred ( R , B , X ) ) $=
      ( wceq ctrpred trpredeq2 syl ) ABCGBDEHCDEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    trpredeq3d.1 $e |- ( ph -> X = Y ) $.
    $( Equality deduction for transitive predecessors.  (Contributed by Scott
       Fenton, 2-Feb-2011.) $)
    trpredeq3d $p |- ( ph -> TrPred ( R , A , X ) = TrPred ( R , A , Y ) ) $=
      ( wceq ctrpred trpredeq3 syl ) ADEGBCDHBCEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d R a y i $.  $d A a y i $.  $d X a y i $.  $d Y a y i $.
    $( A class is a transitive predecessor iff it is in some value of the
       underlying function.  This theorem is not really meant to be used
       directly: instead refer to ~ trpredpred and ~ trpredmintr .
       (Contributed by Scott Fenton, 28-Apr-2012.) $)
    eltrpred $p |- ( Y e. TrPred ( R , A , X ) <->
                    E. i e. om Y e. ( ( rec ( ( a e. _V |->
                                              U_ y e. a Pred ( R , A , y ) ) ,
                                        Pred ( R , A , X ) ) |` om ) ` i ) ) $=
      ( ctrpred wcel com cv cvv cpred ciun cmpt crdg cres cfv wrex dftrpred2
      eleq2i eliun bitri ) FBCEHZIFDJDKGLAGKBCAKMNOBCEMPJQRZNZIFUEIDJSUDUFFABCD
      EGTUADFJUEUBUC $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)
  $}

  ${
    $d A a b d e j $.  $d A a d e y $.  $d B j $.  $d i j $.  $d j y $.
    $d R a b d e j $.  $d R y $.  $d X a d e j $.
    $( Technical lemma for transitive predecessors properties.  All values of
       the transitive predecessors' underlying function are subsets of the base
       set.  (Contributed by Scott Fenton, 28-Apr-2012.) $)
    trpredlem1 $p |- ( Pred ( R , A , X ) e. B ->
                      ( ( rec ( ( a e. _V |-> U_ y e. a Pred ( R , A , y ) ) ,
                                Pred ( R , A , X ) ) |` om ) ` i )
                       C_ A ) $=
      ( vj ve cv com wcel cvv cfv wss c0 wceq sseq1d mpbiri ax-17 vd cpred ciun
      cmpt crdg cres wi csuc wrex wo nn0suc predss fr0g fveq2 syl5ibr iunss a1i
      wa mprgbir eliun hbmpt1 hbrdg hbres hbrex hbxfrbi predeq3 cbviunv mpteq2i
      hbfv rdgeq1 reseq1 mp2b iuneq1 frsucmpt wn 0ss frsucmptn adantl pm2.61dan
      adantr wb mpbird rexlimiva a1d jaoi syl nfvres pm2.61i ) EJZKLZBDFUBZCLZW
      IGMAGJZBDAJZUBZUCZUDZWKUEZKUFZNZBOZUGZWJWIPQZWIHJZUHZQZHKUIZUJXBHWIUKXCXB
      XGWLXAXCPWSNZBOZWLXIWKBOBDFULWLXHWKBWKCWQUMRSXCWTXHBWIPWSUNRUOXGXAWLXFXAH
      KXDKLZXFURXAXEWSNZBOZXJXLXFXJIXDWSNZBDIJZUBZUCZMLZXLXJXQURZXLXPBOZXSXOBOZ
      IXMIXMXOBUPXTXNXMLBDXNULUQUSXRXKXPBGUAWKXDIWMXOUCZXPMWSUAJZWKLGTZYBXDLGTZ
      YBXPLYBXOLZIXMUIGIYBXMXOUTYEGIXMGIXDWSGIWRKGIWKWQGIMWPVAXNWKLGTVBXNKLGTVC
      XNXDLGTVIYEGTVDVEZWQGMYAUDZQWRYGWKUEZQWSYHKUFQGMWPYAAIWMWOXOBDWNXNVFVGVHW
      KWQYGVJWRYHKVKVLZIWMXMXOVMZVNRSXJXQVOZURZXLPBOZBVPZYLXKPBYKXKPQXJGUAWKXDY
      AXPWSYCYDYFYIYJVQVRRSVSVTXFXAXLWAXJXFWTXKBWIXEWSUNRVRWBWCWDWEWFWJVOZXAWLY
      OXAYMYNYOWTPBWIKWRWGRSWDWH $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)

  $}

  ${
    $d R a y z $.  $d A a y z $.  $d X a y z $.
    $( Assuming it exists, the predecessor class is a subset of the transitive
       predecessors.  (Contributed by Scott Fenton, 18-Feb-2011.) $)
    trpredpred $p |- ( Pred ( R , A , X ) e. B ->
                      Pred ( R , A , X ) C_ TrPred ( R , A , X ) ) $=
      ( va vy vz cpred wcel cvv cv ciun cmpt crdg com wbr c0 wceq ax-mp syl crn
      cres cuni ctrpred wss wex cfv fr0g wfun cdm wb wfn frfnom peano1 eleqtrri
      fnfun funbrfvbg mp3an12 mpbid breq1 cla4ev elrng mpbird elssuni df-trpred
      fndm 0ex syl6sseqr ) ACDHZBIZVIEJFEKACFKHLMZVINOUBZUAZUCZACDUDVJVIVMIZVIV
      NUEVJVOGKZVIVLPZGUFZVJQVIVLPZVRVJQVLUGVIRZVSVIBVKUHVLUIZQVLUJZIVJVTVSUKVL
      OULZWAVIVKUMZOVLUPSQOWBUNWCWBORWDOVLVFSUOQVIBVLUQURUSVQVSGQVGVPQVIVLUTVAT
      GVIVLBVBVCVIVMVDTFACDEVEVH $.
      $( [18-Feb-2011] $)
  $}

  ${
    $d R a b i y z $.  $d A a b i y z $.  $d B i z $.  $d X a b i y z $.
    $( The transitive predecessors form a subset of the base class.
       (Contributed by Scott Fenton, 20-Feb-2011.) $)
    trpredss $p |- ( Pred ( R , A , X ) e. B ->
                    TrPred ( R , A , X ) C_ A ) $=
      ( vi va vy cpred wcel ctrpred com cv cvv ciun cmpt crdg cfv dftrpred2 wss
      cres wral trpredlem1 ralrimivw iunss sylibr syl5eqss ) ACDHZBIZACDJEKELFM
      GFLACGLHNOUGPKTQZNZAGACEDFRUHUIASZEKUAUJASUHUKEKGABCEDFUBUCEKUIAUDUEUF $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d A a b f i j t x y $.  $d R a b f i j t x y $.  $d X a b f i j t x y $.
    $d Y a b f i j t y $.
    $( The transitive predecessors are transitive in ` R ` and ` A `
       (Contributed by Scott Fenton, 20-Feb-2011.) $)
    trpredtr $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
                    ( Y e. TrPred ( R , A , X ) ->
                      Pred ( R , A , Y ) C_ TrPred ( R , A , X ) ) ) $=
      ( vi va vy vt vf vj wcel cv cvv cpred ciun com wa wss wceq cmpt crdg cres
      vb ctrpred cfv wrex wral eltrpred csuc simplr peano2 simpr predeq3 sseq2d
      syl ssid rcla4ev ssiun sylancl setlikespec trpredlem1 sseld expcom adantl
      fvex wi syld ralrimiv ad2antrr iunexg sylancr ax-17 ax17el cbviunv iuneq1
      syl5eq cbvmptv rdgeq1 frsucmpt syl2anc sseqtr4d fveq2 dftrpred2 syl6sseqr
      reseq1 mp2b ex rexlimdva syl5bi ) EBCDUEZLEFMZGNHGMZBCHMZOZPZUAZBCDOZUBZQ
      UCZUFZLZFQUGDBLZBCAMONLABUHZRZBCEOZWKSZHBCFDEGUIXEXBXGFQXEWLQLZRZXBXGXIXB
      RZWLUJZQLZXFXKWTUFZSZXGXJXHXLXEXHXBUKZWLULUPXJXFIXABCIMZOZPZXMXJXBXFXFSZX
      FXRSZXIXBUMXFUQXBXSRXFXQSZIXAUGXTYAXSIEXAXPETXQXFXFBCXPEUNUOURIXAXQXFUSUP
      UTXJXHXRNLZXMXRTXOXJXANLXQNLZIXAUHZYBWLWTVFXEYDXHXBXEYCIXAXEXPXALXPBLZYCX
      EXABXPXEWRNLXABSABCDVAHBNCFDGVBUPVCXDYEYCVGXCYEXDYCABCXPVAVDVEVHVIVJIXAXQ
      NNVKVLJUDWRWLIJMZXQPZXRNWTUDMZWRLJVMUDFJVNYHXRLJVMWQJNYGUAZTWSYIWRUBZTWTY
      JQUCTGJNWPYGWMYFTWPIWMXQPYGHIWMWOXQBCWNXPUNVOIWMYFXQVPVQVRWRWQYIVSWSYJQWF
      WGIYFXAXQVPVTWAWBXLXNRZXFKQKMZWTUFZPZWKYKXFYMSZKQUGXFYNSYOXNKXKQYLXKTYMXM
      XFYLXKWTWCUOURKQYMXFUSUPHBCKDGWDWEWAWHWIWJ $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.  $d B y $.  $d A x y a b c d i j k $.
    $d B i j k $.  $d R a b c d i j k $.  $d X a b c i j k $.
    $( The transitive predecessors form the smallest class transitive in ` R `
       and ` A ` .  That is, if ` B ` is another ` R ` , ` A ` transitive class
       containing ` Pred ( R , A , X ) ` , then ` TrPred ( R , A , X ) C_ B `
       (Contributed by Scott Fenton, 25-Apr-2012.) $)
    trpredmintr $p |- ( ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                         ( A. y e. B Pred ( R , A , y ) C_ B /\
                           Pred ( R , A , X ) C_ B ) ) ->
                       TrPred ( R , A , X ) C_ B ) $=
      ( vi va vk vd wcel cv cvv wa wss com cfv wi wceq ax-17 vj vc wral ctrpred
      vb cpred ciun cmpt crdg cres dftrpred2 c0 fveq2 sseq1d imbi2d setlikespec
      csuc weq fr0g syl adantr simprr eqsstrd fvex trpredlem1 sseld expcom syld
      adantl ralrimiv ad2antrr iunexg sylancr wel predeq3 cbviunv iuneq1 syl5eq
      eqid rdgeq1 reseq1 mp2b fveq1i eqeq2i sylbi frsucmpt sylan2 sseq1i anbi2i
      cbvmptv hbra1 hban ssel ra4 ad2antrl sylan9r ralrimi sylan2b iunss sylibr
      exp32 a2d finds com12 syl5eqss ) FCKZCEALUFMKACUCZNZCEBLZUFZDOZBDUCZCEFUF
      ZDOZNZNZCEFUDGPGLZHMBHLZXJUGZUHZXMUIZPUJZQZUGZDBCEGFHUKXPYCDOZGPUCYDDOXPY
      EGPXQPKXPYEXPUALZYBQZDOZRXPULYBQZDOZRXPILZYBQZDOZRXPYKUQZYBQZDOZRXPYERUAI
      XQYFULSZYHYJXPYQYGYIDYFULYBUMUNUOUAIURZYHYMXPYRYGYLDYFYKYBUMUNUOYFYNSZYHY
      PXPYSYGYODYFYNYBUMUNUOUAGURZYHYEXPYTYGYCDYFXQYBUMUNUOXPYIXMDXHYIXMSZXOXHX
      MMKZUUAACEFUPZXMMXTUSUTVAXHXLXNVBVCYKPKZXPYMYPUUDXPYMYPUUDXPYMNZNZYOBYKUB
      MJUBLZCEJLZUFZUGZUHZXMUIZPUJZQZXJUGZDUUEUUDUUOMKZYOUUOSUUEUUNMKXJMKZBUUNU
      CZUUPYKUUMVDXHUURXOYMXHUUQBUUNXHXIUUNKZXICKZUUQXHUUNCXIXHUUBUUNCOUUCJCMEI
      FUBVEUTVFXGUUTUUQRXFUUTXGUUQACEXIUPVGVIVHVJVKBUUNXJMMVLVMHUEXMYKXSUUOMYBU
      ELZXMKHTUEIVNHTUVAUUOKHTYBVSXRYLSXRUUNSXSUUOSYLUUNXRYKYBUUMXTUUKSYAUULSYB
      UUMSHUBMXSUUJHUBURXSJXRUUIUGUUJBJXRXJUUICEXIUUHVOVPJXRUUGUUIVQVRWJXMXTUUK
      VTYAUULPWAWBWCZWDBXRUUNXJVQWEWFWGUUFXKBUUNUCZUUODOUUEUUDXPUUNDOZNZUVCYMUV
      DXPYLUUNDUVBWHWIUVEUVCUUDUVEXKBUUNXPUVDBXHXOBXHBTXLXNBXKBDWKXNBTWLWLUVDBT
      WLUVDUUSXIDKZXPXKUUNDXIWMXLUVFXKRXHXNXKBDWNWOWPWQVIWRBUUNXJDWSWTVCXAXBXCX
      DVJGPYCDWSWTXE $.
      $( [12-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.  $d Y x y $.
    $( Given a transitive predecessor ` Y ` of ` X ` , the transitive
       predecessors of ` Y ` are a subset of the transitive predecessors of
       ` X ` .  (Contributed by Scott Fenton, 25-Apr-2012.) $)
    trpredelss $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
                      ( Y e. TrPred ( R , A , X ) ->
                        TrPred ( R , A , Y ) C_ TrPred ( R , A , X ) ) ) $=
      ( vy wcel cv cpred cvv wral wa ctrpred setlikespec trpredss sselda simplr
      wss syl trpredtr ralrimiv adantr imp trpredmintr syl22anc ex ) DBGZBCAHIJ
      GABKZLZEBCDMZGZBCEMUJRZUIUKLEBGUHBCFHZIUJRZFUJKZBCEIUJRZULUIUJBEUIBCDIJGU
      JBRABCDNBJCDOSPUGUHUKQUIUOUKUIUNFUJABCDUMTUAUBUIUKUPABCDETUCAFBUJCEUDUEUF
      $.
      $( [12-May-2012] $) $( [25-Apr-2012] $)
  $}

  ${
    $d A x y z $.  $d R x y z $.  $d X x y z $.
    $( The transitive predecessors of ` X ` are equal to the predecessors of
       ` X ` together with their transitive predecessors.  (Contributed by
       Scott Fenton, 26-Apr-2012.) $)
    dftrpred3g $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
                    TrPred ( R , A , X ) =
                    ( Pred ( R , A , X ) u.
                      U_ y e. Pred ( R , A , X ) TrPred ( R , A , y ) ) ) $=
      ( vz wcel cv cpred cvv wral ctrpred wss predel setlikespec trpredpred syl
      wa wi syl6 ciun cun wo elun expcom adantl syl5 ancld weq trpredeq3 sseq2d
      wrex rcla4ev ssiun eliun ancoms adantll trpredss sseld ad2antlr imp simpr
      syld simplr trpredelss syl2anc sstrd exp31 reximdvai syl5bi jaod ralrimiv
      ssun4 ssun1 jctir trpredmintr mpdan iunss sylibr unssd eqssd ) ECGZCDAHIJ
      GACKZRZCDELZCDEIZBWFCDBHZLZUAZUBZWDCDFHZIZWJMZFWJKZWFWJMZRWEWJMWDWNWOWDWM
      FWJWKWJGWKWFGZWKWIGZUCZWDWMWKWFWIUDWDWRWLWIMZWMWDWPWSWQWDWPWPWLCDWKLZMZRZ
      WSWDWPXAWPWKCGZWDXACDEWKNWCXCXASWBXCWCXAXCWCRWLJGZXAACDWKOZCJDWKPZQUEUFUG
      UHXBWLWHMZBWFULZWSXGXABWKWFBFUIWHWTWLCDWGWKUJUKUMBWFWHWLUNZQTWQWKWHGZBWFU
      LZWDWSBWKWFWHUOWDXKXHWSWDXJXGBWFWGWFGZWGCGZWDXJXGSCDEWGNWDXMXJXGWDXMRZXJR
      ZWLWTWHXOXDXAXNXJXDXNXJXCXDXNWHCWKXNCDWGIJGZWHCMWCXMXPWBXMWCXPACDWGOUPUQC
      JDWGURQUSWCXCXDSWBXMXCWCXDXEUEUTVCVAXFQXNXJWTWHMZXNXMWCXJXQSWDXMVBWBWCXMV
      DACDWGWKVEVFVAVGVHUGVIXITVJVKWLWIWFVMTVJVLWFWIVNVOAFCWJDEVPVQWDWFWIWEWDWF
      JGWFWEMACDEOCJDEPQZWDWHWEMZBWFKWIWEMWDXSBWFWDXLWGWEGXSWDWFWEWGXRUSACDEWGV
      EVCVLBWFWHWEVRVSVTWA $.
      $( [12-May-2012] $) $( [26-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.
    $( Another recursive expression for the transitive predecessors.
       (Contributed by Scott Fenton, 27-Apr-2012.) $)
    dftrpred4g $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
        TrPred ( R , A , X ) =
        U_ y e. Pred ( R , A , X ) ( { y } u. TrPred ( R , A , y ) ) ) $=
      ( wcel cv cpred cvv wral wa ctrpred cun csn dftrpred3g iunun iunid uneq1i
      ciun eqtri syl6eqr ) ECFCDAGHIFACJKCDELCDEHZBUBCDBGZLZSZMZBUBUCNZUDMSZABC
      DEOUHBUBUGSZUEMUFBUBUGUDPUIUBUEBUBQRTUA $.
      $( [12-May-2012] $) $( [27-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.
    $( If ` R ` partially orders ` A ` , then the transitive predecessors are
       the same as the immediate predecessors .  (Contributed by Scott Fenton,
       28-Apr-2012.) $)
    trpredpo $p |- ( ( R Po A /\ X e. A /\
                        A. x e. A Pred ( R , A , x ) e. _V ) ->
                      TrPred ( R , A , X ) = Pred ( R , A , X ) ) $=
      ( vy wpo wcel cv cpred cvv w3a ctrpred wss simp2 simp3 wa predpo ralrimiv
      wral 3adant3 ssid a1i trpredmintr syl22anc setlikespec trpredpred 3adant1
      syl eqssd ) BCFZDBGZBCAHIJGABSZKZBCDLZBCDIZUMUKULBCEHZIUOMZEUOSZUOUOMZUNU
      OMUJUKULNUJUKULOUJUKURULUJUKPUQEUOBCDUPQRTUSUMUOUAUBAEBUOCDUCUDUKULUOUNMZ
      UJUKULPUOJGUTABCDUEBJCDUFUHUGUI $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)
  $}

  ${
    $d R a i j k y z $.  $d X a i j k y z $.
    $( The class of transitive predecessors is empty when ` A ` is empty.
       (Contributed by Scott Fenton, 30-Apr-2012.) $)
    trpred0 $p |- TrPred ( R , (/) , X ) = (/) $=
      ( vi va vy vj c0 com cv cvv cpred ciun cmpt crdg cres cfv wcel wceq pred0
      wel ctrpred dftrpred2 a1i iuneq2i iun0 eqtri mpteq2i rdgeq12 mp2an fveq1i
      reseq1i csuc wrex nn0suc fveq2 0ex fr0g ax-mp syl6eq ax-17 eqidd frsucmpt
      wo eqid mpan2 eqeq1d syl5ibrcom rexlimiv jaoi syl syl5eq 3eqtri ) GABUACH
      CIZDJEDIZGAEIZKZLZMZGABKZNZHOZPZLCHGLGEGACBDUBCHWBGVMHQZWBVMDJGMZGNZHOZPZ
      GVMWAWFVTWEHVRWDRVSGRVTWERDJVQGVQEVNGLGEVNVPGVPGREDTAVOSUCUDEVNUEUFUGABSV
      SGVRWDUHUIUKUJWCVMGRZVMFIZULZRZFHUMZVCWGGRZFVMUNWHWMWLWHWGGWFPZGVMGWFUOGJ
      QZWNGRUPGJWDUQURUSWKWMFHWIHQZWMWKWJWFPZGRZWPWOWRUPDCGWIGGJWFVMGQDUTZCFTDU
      TWSWFVDVNWIWFPRGVAVBVEWKWGWQGVMWJWFUOVFVGVHVIVJVKUDCHUEVL $.
      $( [12-May-2012] $) $( [30-Apr-2012] $)
  $}

  ${
    $d R a y $.  $d A a y $.  $d X a y $.
    $( The transitive predecessors of a relation form a set (NOTE: this is the
       first theorem in the transitive predecessor series that requires
       infinity).  (Contributed by Scott Fenton, 18-Feb-2011.) $)
    trpredex $p |- TrPred ( R , A , X ) e. _V $=
      ( va vy ctrpred cvv cpred ciun cmpt crdg com cres crn cuni df-trpred wcel
      cv wfn frfnom omex fnex mp2an rnex uniex eqeltri ) ABCFDGEDRABERHIJZABCHZ
      KLMZNZOGEABCDPUJUIUILSLGQUIGQUHUGTUALGUIUBUCUDUEUF $.
      $( [18-Feb-2011] $)
  $}

  ${
    $d A x z $.  $d R x z $.  $d X x z $.  $d Y z $.  $d A a i j y x z $.
    $d R a i j y $.  $d X a i j y $.  $d Y a i j y $.
    $( If ` Y ` is an ` R ` , ` A ` transitive predecessor, then it is either
       an immediate predecessor or there is a transitive predecessor between
       ` Y ` and ` X ` (Contributed by Scott Fenton, 9-May-2012.) $)
    trpredrec $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
       ( Y e. TrPred ( R , A , X ) -> ( Y e. Pred ( R , A , X ) \/
         E. z e. TrPred ( R , A , X ) Y R z ) ) ) $=
      ( vi va vy vj wcel cv cvv cpred com wrex wa wceq wi ax-17 ctrpred cfv wbr
      ciun cmpt crdg cres wral wo eltrpred c0 nn0suc fveq2 eleq2d anbi2d biimpd
      csuc setlikespec fr0g syl biimpa syl6com fvex wss trpredlem1 sseld expcom
      adantl syld ralrimiv iunexg sylancr eliun hbmpt1 hbrdg hbres hbfv hbxfrbi
      wel hbrex predeq3 cbviunv iuneq1 syl5eq frsucmpt sylan2 expimpd dftrpred2
      eqid ssiun2 syl6sseqr vex elpredim a1i anim12d reximdv2 com12 sylbi com23
      pm2.43d rexlimdv orim12d ex syl5 syl5bi ) FCDEUAZKFGLZHMIHLZCDILZNZUDZUEZ
      CDENZUFZOUGZUBZKZGOPECKZCDALNMKACUHZQZFXMKZFBLZDUCZBXFPZUIZICDGEFHUJXTXQY
      EGOXGOKXGUKRZXGJLZUQZRZJOPZUIZXTXQYESJXGULXTXQYKYEXTXQYKYESXTXQQZYFYAYJYD
      YFYLXTFUKXOUBZKZQZYAYFYLYOYFXQYNXTYFXPYMFXGUKXOUMUNUOUPXTYNYAXTYMXMFXTXMM
      KZYMXMRACDEURZXMMXLUSUTUNVAVBYLYIYDJOYLYIYGOKZYDYIYLXTFYHXOUBZKZQZYRYDSZY
      IYLUUAYIXQYTXTYIXPYSFXGYHXOUMUNUOUPUUAYRYDYRUUAFBYGXOUBZCDYBNZUDZKZUUBYRX
      TYTUUFYRXTQZYTUUFUUGYSUUEFXTYRUUEMKZYSUUERXTUUCMKUUDMKZBUUCUHUUHYGXOVCXTU
      UIBUUCXTYBUUCKZYBCKZUUIXTUUCCYBXTYPUUCCVDYQICMDJEHVEUTVFXSUUKUUISXRUUKXSU
      UIACDYBURVGVHVIVJBUUCUUDMMVKVLHGXMYGXKUUEMXOXGXMKHTGJVSHTXGUUEKXGUUDKZBUU
      CPHBXGUUCUUDVMUULHBUUCHBYGXOHBXNOHBXMXLHBMXKVNYBXMKHTVOYBOKHTVPBJVSHTVQUU
      LHTVTVRXOWIXHUUCRXKBXHUUDUDUUEIBXHXJUUDCDXIYBWAWBBXHUUCUUDWCWDWEWFUNUPWGU
      UFFUUDKZBUUCPZUUBBFUUCUUDVMYRUUNYDYRUUMYCBUUCXFYRUUJYBXFKUUMYCYRUUCXFYBYR
      UUCJOUUCUDXFJOUUCWJICDJEHWHWKVFUUMYCSYRCDYBFBWLWMWNWOWPWQWRVBWTVBWSXAXBXC
      WSXDXAXE $.
      $( [12-May-2012] $) $( [9-May-2012] $)
  $}

  $(
  @{ @d R x z @. @d A x z @. @d X x z @.
     trpredfrirr.1 @e |- R Fr A @.
     trpredfrirr.2 @e |- A. x e. A Pred ( R , A , x ) e. _V @.

     @( If ` R ` is founded and set-like over ` A ` , then the transitive
        predecessor set is irreflexive @)
     trpredfrirr @p |- ( X e. A -> -. X e. TrPred ( R , A , X ) ) @=
       ( vz wcel ctrpred cpred cv wbr wrex wo wn wa nrexdv wfr predfrirr ax-mp
       jctil ioran sylibr cvv wral wi trpredrec mpan2 mtod ) DBHZDBCDIZHZDBCDJH
       ZDGKCLZGUKMZNZUJUMOZUOOZPUPOUJURUQUJUNGUK?QBCRUQEBCDSTUAUMUOUBUCUJBCAKJU
       DHABUEULUPUFFAGBCDDUGUHUI @.
       @( [12-May-2012] @) @( [10-May-2012] @)
  @}
  $)

