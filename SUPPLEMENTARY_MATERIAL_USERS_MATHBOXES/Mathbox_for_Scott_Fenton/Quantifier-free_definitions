$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Quantifier-free definitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (x) Bigcup SSet Trans Limits Fix Funs Singleton Singletons Image Cart $.
  $c Img Domain Range pprod Apply Cup Cap Succ Funpart FullFun Restrict $.
  $( Declare the syntax for tail cross product. $)
  ctxp $a class ( A (x) B ) $.
  $( Declare the syntax for the parallel product. $)
  cpprod $a class pprod ( R , S ) $.
  $( Declare the subset relationship class. $)
  csset $a class SSet $.
  $( Declare the transitive set class. $)
  ctrans $a class Trans $.
  $( Declare the set union relationship. $)
  cbigcup $a class Bigcup $.
  $( Declare the class of limit ordinals. $)
  climits $a class Limits $.
  $( Declare the syntax for the fixpoints of a class. $)
  cfix $a class Fix A $.
  $( Declare the syntax for the class of all function. $)
  cfuns $a class Funs $.
  $( Declare the syntax for the singleton function. $)
  csingle $a class Singleton $.
  $( Declare the syntax for the class of all singletons. $)
  csingles $a class Singletons $.
  $( Declare the syntax for the image functor. $)
  cimage $a class Image A $.
  $( Declare the syntax for the cartesian function. $)
  ccart $a class Cart $.
  $( Declare the syntax for the image function. $)
  cimg $a class Img $.
  $( Declare the syntax for the domain function. $)
  cdomain $a class Domain $.
  $( Declare the syntax for the range function. $)
  crange $a class Range $.
  $( Declare the syntax for the application function. $)
  capply $a class Apply $.
  $( Declare the syntax for the cup function. $)
  ccup $a class Cup $.
  $( Declare the syntax for the cap function. $)
  ccap $a class Cap $.
  $( Declare the syntax for the successor function. $)
  csuccf $a class Succ $.
  $( Declare the syntax for the functional part functor. $)
  cfunpart $a class Funpart F $.
  $( Declare the syntax for the full function functor. $)
  cfullfn $a class FullFun F $.
  $( Declare the syntax for the restriction function. $)
  crestrict $a class Restrict $.

  $( Define the tail cross of two classes.  Membership in this class is defined
     by ~ txpss3v and ~ brtxp . $)
  df-txp $a |- ( A (x) B ) = ( ( `' ( 1st |` ( _V X. _V ) ) o. A ) i^i
                               ( `' ( 2nd |` ( _V X. _V ) ) o. B ) ) $.

  $( Define the parallel product of two classes.  Membership in this class is
     defined by ~ pprodss4v and ~ brpprod . $)
  df-pprod $a |- pprod ( A , B ) =
     ( ( A o. ( 1st |` ( _V X. _V ) ) ) (x)
       ( B o. ( 2nd |` ( _V X. _V ) ) ) ) $.

  $( Define the subset class.  For the value, see ~ brsset . $)
  df-sset $a |- SSet = ( ( _V X. _V ) \ ran ( _E (x) ( _V \ _E ) ) ) $.

  $( Define the class of all transitive sets. $)
  df-trans $a |- Trans = ( _V \ ran ( ( _E o. _E ) \ _E ) ) $.

  $( Define the Bigcup function, which, per ~ fvbigcup , carries a set to its
     union. $)
  df-bigcup $a |- Bigcup = ( ( _V X. _V ) \
     ran ( ( _V (x) _E ) (++) ( ( _E o. _E ) (x) _V ) ) ) $.

  $( Define the class of all fixpoints of a relationship. $)
  df-fix $a |- Fix A = dom ( A i^i _I ) $.

  $( Define the class of all limit ordinals. $)
  df-limits $a |- Limits = ( ( On i^i Fix Bigcup ) \ { (/) } ) $.

  $( Define the class of all functions.  See ~ elfuns for membership. $)
  df-funs $a |- Funs =
    ( ~P ( _V X. _V ) \
      Fix ( _E o. ( ( 1st (x) ( ( _V \ _I ) o. 2nd ) ) o. `' _E ) ) ) $.

  $( Define the singleton function.  See ~ brsingle for its value. $)
  df-singleton $a |- Singleton =
     ( ( _V X. _V ) \ ran ( ( _V (x) _E ) (++) ( _I (x) _V ) ) ) $.

  $( Define the class of all singletons.  See ~ elsingles for membership. $)
  df-singles $a |- Singletons = ran Singleton $.

  $( Define the image functor.  This function takes a set ` A ` to a function
     ` x |-> ( A " x ) ` , providing that the latter exists.  See ~ imageval
     for the derivation. $)
  df-image $a |- Image A = ( ( _V X. _V ) \
           ran ( ( _V (x) _E ) (++) ( ( _E o. `' A ) (x) _V ) ) ) $.

  $( Define the cartesian product function.  See ~ brcart for its value. $)
  df-cart $a |- Cart = ( ( ( _V X. _V ) X. _V ) \
    ran ( ( _V (x) _E ) (++) ( pprod ( _E , _E ) (x) _V ) ) ) $.

  $( Define the image function.  See ~ brimg for its value. $)
  df-img $a |- Img = ( Image ( ( 2nd o. 1st ) |` ( 1st |` ( _V X. _V ) ) )
                       o. Cart ) $.

  $( Define the domain function.  See ~ brdomain for its value. $)
  df-domain $a |- Domain = Image ( 1st |` ( _V X. _V ) ) $.

  $( Define the range function.  See ~ brrange for its value. $)
  df-range $a |- Range = Image ( 2nd |` ( _V X. _V ) ) $.

  $( Define the little cup function.  See ~ brcup for its value. $)
  df-cup $a |- Cup = ( ( ( _V X. _V ) X. _V ) \
  ran ( ( _V (x) _E ) (++)
        ( ( ( `' 1st o. _E ) u. ( `' 2nd o. _E ) ) (x) _V ) ) ) $.

  $( Define the little cap function.  See ~ brcap for its value. $)
  df-cap $a |- Cap = ( ( ( _V X. _V ) X. _V ) \
  ran ( ( _V (x) _E ) (++)
        ( ( ( `' 1st o. _E ) i^i ( `' 2nd o. _E ) ) (x) _V ) ) ) $.

  $( Define the restriction function.  See ~ brrestrict for its value. $)
  df-restrict $a |- Restrict = ( Cap o.
    ( 1st (x) ( Cart o. ( 2nd (x) ( Range o. 1st ) ) ) ) ) $.

  $( Define the successor function.  See ~ brsuccf for its value. $)
  df-succf $a |- Succ = ( Cup o. ( _I (x) Singleton ) ) $.

  $( Define the application function.  See ~ brapply for its value. $)
  df-apply $a |- Apply = ( ( Bigcup o. Bigcup ) o.
  ( ( ( _V X. _V ) \
      ran ( ( _V (x) _E ) (++) ( ( _E |` Singletons ) (x) _V ) ) ) o.
  ( ( Singleton o. Img ) o. pprod ( _I , Singleton ) ) ) ) $.


  $( Define the functional part of a class ` F ` .  This is the maximal part of
     ` F ` that is a function.  See ~ funpartfun and ~ funpartfv for the
     meaning of this statement. $)
  df-funpart $a |- Funpart F =
             ( F |` dom ( ( Image F o. Singleton ) i^i ( _V X. Singletons ) ) )
      $.

  $( Define the full function over ` F ` .  This is a function with domain
     ` _V ` that always agrees with ` F ` for its value. $)
  df-fullfun $a |- FullFun F = ( Funpart F u. ( ( _V \ dom Funpart F ) X. { (/)
      } ) ) $.

  $( The binary relationship over ` _V ` always holds.  (Contributed by Scott
     Fenton, 11-Apr-2012.) $)
  brv $p |- A _V B $=
    ( cvv wbr cop wcel opex df-br mpbir ) ABCDABECFABGABCHI $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    $( A tail cross product is a subset of the class of ordered triples.
       (Contributed by Scott Fenton, 31-Mar-2012.) $)
    txpss3v $p |- ( A (x) B ) C_ ( _V X. ( _V X. _V ) ) $=
      ( vx vy vz ctxp c1st cvv cxp cres ccnv ccom c2nd cin df-txp inss1 cv wcel
      wbr vex relco wa wex cop brcnv brres simprbi adantl exlimiv opelco opelxp
      sylbi mpbiran 3imtr4i relssi sstri eqsstri ) ABFGHHIZJZKZALZMURJKBLZNZHUR
      IZABOVCVAVDVAVBPCDVAVDUTAUACQZEQZASZVFDQZUTSZUBZEUCVHURRZVEVHUDZVARVLVDRZ
      VJVKEVIVKVGVIVHVFUSSZVKVFVHUSETZDTZUEVNVHVFGSVKVHVFGURVOUFUGULUHUIEVEVHUT
      ACTZVPUJVMVEHRVKVQVEVHHURVPUKUMUNUOUPUQ $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  $( A tail cross product is a relationship.  (Contributed by Scott Fenton,
     31-Mar-2012.) $)
  txprel $p |- Rel ( A (x) B ) $=
    ( ctxp wrel cvv cxp wss txpss3v xpss sstri df-rel mpbir ) ABCZDMEEFZGMENFNA
    BHENIJMKL $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A a x y z $.  $d B a x y z $.  $d X a x y z $.  $d Y a x y z $.
    $d Z a x y z $.
    brtxp.1 $e |- X e. _V $.
    brtxp.2 $e |- Y e. _V $.
    brtxp.3 $e |- Z e. _V $.
    $( Characterize a trinary relationship over a tail cross product.  Together
       with ~ txpss3v , this completely defines membership in a tail cross.
       (Contributed by Scott Fenton, 31-Mar-2012.) $)
    brtxp $p |- ( X ( A (x) B ) <. Y , Z >. <-> ( X A Y /\ X B Z ) ) $=
      ( vy vz wbr c1st cvv cres ccnv ccom c2nd wa wex 3bitri cop cxp cin df-txp
      ctxp breqi brin wceq opex brco ancom vex brcnv wcel opelvv brres mpbiran2
      cv br1steq anbi1i bitri exbii breq2 ceqsexv br2ndeq anbi12i ) CDEUAZABUEZ
      KCVGLMMUBZNZOZAPZQVINZOZBPZUCZKCVGVLKZCVGVOKZRCDAKZCEBKZRCVGVHVPABUDUFCVG
      VLVOUGVQVSVRVTVQCIURZAKZWAVGVKKZRZISWADUHZWBRZISVSICVGVKAFDEUIZUJWDWFIWDW
      CWBRWFWBWCUKWCWEWBWCVGWAVJKZVGWALKZWEWAVGVJIULZWGUMWHWIVGVIUNZDEGUOZVGWAL
      VIWJUPUQDEWAGHWJUSTUTVAVBWBVSIDGWADCAVCVDTVRCJURZBKZWMVGVNKZRZJSWMEUHZWNR
      ZJSVTJCVGVNBFWGUJWPWRJWPWOWNRWRWNWOUKWOWQWNWOVGWMVMKZVGWMQKZWQWMVGVMJULZW
      GUMWSWTWKWLVGWMQVIXAUPUQDEWMGHXAVETUTVAVBWNVTJEHWMECBVCVDTVFT $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d R x y $.  $d S x y $.
    brtxp2.1 $e |- A e. _V $.
    brtxp2.2 $e |- B e. _V $.
    $( The binary relationship over a tail cross when the second argument is
       not an ordered pair.  (Contributed by Scott Fenton, 14-Apr-2014.) $)
    brtxp2 $p |- ( A ( R (x) S ) B <->
            E. x E. y ( B = <. x , y >. /\ A R x /\ A S y ) ) $=
      ( ctxp wbr cv cop wceq wa wex cvv wcel bitr4i 2exbii vex w3a txpss3v brel
      cxp simprd elvv sylib pm4.71ri 19.41vv breq2 pm5.32i anbi2i 3anass 3bitri
      brtxp ) CDEFIZJZDAKZBKZLZMZUQNZBOAOZVACUTUPJZNZBOAOVACUREJZCUSFJZUAZBOAOU
      QVABOAOZUQNVCUQVIUQDPPUDZQZVIUQCPQVKCDPVJUPHEFUBUCUEABDUFUGUHVAUQABUIRVBV
      EABVAUQVDDUTCUPUJUKSVEVHABVEVAVFVGNZNVHVDVLVAEFCURUSGATBTUOULVAVFVGUMRSUN
      $.
      $( [19-Apr-2014] $) $( [14-Apr-2014] $)
  $}

  $( Expanded definition of parallel product.  (Contributed by Scott Fenton,
     3-May-2014.) $)
  dfpprod2 $p |- pprod ( A , B ) =
     ( ( `' ( 1st |` ( _V X. _V ) ) o. ( A o. ( 1st |` ( _V X. _V ) ) ) ) i^i
       ( `' ( 2nd |` ( _V X. _V ) ) o. ( B o. ( 2nd |` ( _V X. _V ) ) ) ) ) $=
    ( cpprod c1st cvv cxp cres ccom c2nd ctxp ccnv cin df-pprod df-txp eqtri )
    ABCADEEFZGZHZBIPGZHZJQKRHSKTHLABMRTNO $.
    $( [3-May-2014] $)

  $( A converse law for parallel product.  (Contributed by Scott Fenton,
     3-May-2014.) $)
  pprodcnveq $p |- pprod ( R , S ) = `' pprod ( `' R , `' S ) $=
    ( c1st cvv cxp cres ccnv ccom c2nd cpprod cnvin cnvco1 coeq1i coass ineq12i
    cin 3eqtri eqtr2i dfpprod2 cnveqi 3eqtr4i ) CDDEZFZGZAUCHHZIUBFZGZBUFHHZPZU
    DAGZUCHZHZUGBGZUFHZHZPZGZABJUJUMJZGUQULGZUOGZPUIULUOKUSUEUTUHUSUKGZUCHUDAHZ
    UCHUEUCUKLVAVBUCAUCLMUDAUCNQUTUNGZUFHUGBHZUFHUHUFUNLVCVDUFBUFLMUGBUFNQORABS
    URUPUJUMSTUA $.
    $( [3-May-2014] $)

  ${
    $d A x y z w $.  $d B x y z w $.
    $( The parallel product is a subclass of
       ` ( ( _V X. _V ) X. ( _V X. _V ) ) ` .  (Contributed by Scott Fenton,
       11-Apr-2014.) $)
    pprodss4v $p |- pprod ( A , B ) C_ ( ( _V X. _V ) X. ( _V X. _V ) ) $=
      ( vx vy vz vw c1st cvv cxp cres ccom cv cop wcel vex opelxp simprbi sylbi
      wex wbr cpprod c2nd ctxp df-pprod txprel txpss3v sseli wceq wi elvv opeq2
      syl eleq1d df-br wa brtxp brco brres adantr exlimiv sylbir exlimivv mpcom
      syl6bi sylanbrc relssi eqsstri ) ABUAAGHHIZJZKZBUBVHJKZUCZVHVHIZABUDCDVLV
      MVJVKUECLZDLZMZVLNZVNVHNZVOVHNZVPVMNVSVQVRVQVPHVHIZNZVSVLVTVPVJVKUFUGWAVN
      HNVSVNVOHVHDOZPQULZVSVOELZFLZMZUHZFSESVQVRUIZEFVOUJWGWHEFWGVQVNWFMZVLNZVR
      WGVPWIVLVOWFVNUKUMWJVNWFVLTZVRVNWFVLUNWKVNWDVJTZVNWEVKTZUOVRVJVKVNWDWECOZ
      EOZFOUPWLVRWMWLVNVOVITZVOWDATZUOZDSVRDVNWDAVIWNWOUQWRVRDWPVRWQWPVNVOGTVRV
      NVOGVHWBURQUSUTRUSRVAVDVBRVCWCVNVOVHVHWBPVEVFVG $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)
  $}

  ${
    $d A x $.  $d B y $.  $d W y $.  $d X x $.  $d X y $.  $d Y x $.  $d Y y $.
    $d Z x $.
    brpprod.1 $e |- X e. _V $.
    brpprod.2 $e |- Y e. _V $.
    brpprod.3 $e |- Z e. _V $.
    brpprod.4 $e |- W e. _V $.
    $( Characterize a quatary relationship over a tail cross product.  Together
       with ~ pprodss4v , this completely defines membership in a parallel
       product.  (Contributed by Scott Fenton, 11-Apr-2014.) $)
    brpprod $p |- ( <. X , Y >. pprod ( A , B ) <. Z , W >. <->
            ( X A Z /\ Y B W ) ) $=
      ( vx vy cop wbr c1st cvv c2nd wa wex 3bitri cpprod cxp cres ccom df-pprod
      ctxp breqi opex brtxp cv wceq brco wcel opelvv vex brres mpbiran2 br1steq
      bitri anbi1i exbii breq1 ceqsexv br2ndeq anbi12i ) DEMZFCMZABUAZNVFVGAOPP
      UBZUCZUDZBQVIUCZUDZUFZNVFFVKNZVFCVMNZRDFANZECBNZRVFVGVHVNABUEUGVKVMVFFCDE
      UHZIJUIVOVQVPVRVOVFKUJZVJNZVTFANZRZKSVTDUKZWBRZKSVQKVFFAVJVSIULWCWEKWAWDW
      BWAVFVTONZWDWAWFVFVIUMZDEGUNZVFVTOVIKUOZUPUQDEVTGHWIURUSUTVAWBVQKDGVTDFAV
      BVCTVPVFLUJZVLNZWJCBNZRZLSWJEUKZWLRZLSVRLVFCBVLVSJULWMWOLWKWNWLWKVFWJQNZW
      NWKWPWGWHVFWJQVILUOZUPUQDEWJGHWQVDUSUTVAWLVRLEHWJECBVBVCTVET $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)
  $}

  ${
    $d a b $.  $d a w $.  $d a z $.  $d b w $.  $d b z $.  $d R a $.  $d R b $.
    $d R w $.  $d R z $.  $d S a $.  $d S b $.  $d S w $.  $d S z $.  $d w z $.
    $d X a $.  $d X b $.  $d X w $.  $d X z $.  $d Y a $.  $d Y b $.  $d Y w $.
    $d Y z $.  $d Z a $.  $d Z b $.  $d Z w $.  $d Z z $.
    brpprod3.1 $e |- X e. _V $.
    brpprod3.2 $e |- Y e. _V $.
    brpprod3.3 $e |- Z e. _V $.
    $( Condition for parallel product when the last argument is not an ordered
       pair.  (Contributed by Scott Fenton, 11-Apr-2014.) $)
    brpprod3a $p |- ( <. X , Y >. pprod ( R , S ) Z <->
       E. z E. w ( Z = <. z , w >. /\ X R z /\ Y S w ) ) $=
      ( cop wbr cv wa wex cvv wcel bitr4i 2exbii vex cpprod wceq pprodss4v brel
      w3a cxp simprd sylib pm4.71ri 19.41vv breq2 pm5.32i brpprod anbi2i 3anass
      elvv 3bitri ) EFKZGCDUAZLZGAMZBMZKZUBZUTNZBOAOZVDURVCUSLZNZBOAOVDEVACLZFV
      BDLZUEZBOAOUTVDBOAOZUTNVFUTVLUTGPPUFZQZVLUTURVMQVNURGVMVMUSJCDUCUDUGABGUP
      UHUIVDUTABUJRVEVHABVDUTVGGVCURUSUKULSVHVKABVHVDVIVJNZNVKVGVOVDCDVBEFVAHIA
      TBTUMUNVDVIVJUORSUQ $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)

    $( Condition for parallel product when the first argument is not an ordered
       pair.  (Contributed by Scott Fenton, 3-May-2014.) $)
    brpprod3b $p |- ( X pprod ( R , S ) <. Y , Z >. <->
       E. z E. w ( X = <. z , w >. /\ z R Y /\ w S Z ) ) $=
      ( cop cpprod wbr ccnv cv wceq w3a wex brcnv vex pprodcnveq opex brpprod3a
      breqi bitri biid 3anbi123i 2exbii 3bitri ) EFGKZCDLZMEUJCNZDNZLZNZMZEAOZB
      OZKPZFUQULMZGURUMMZQZBRARZUSUQFCMZURGDMZQZBRAREUJUKUOCDUAUDUPUJEUNMVCEUJU
      NHFGUBSABULUMFGEIJHUCUEVBVFABUSUSUTVDVAVEUSUFFUQCIATSGURDJBTSUGUHUI $.
      $( [3-May-2014] $)

  $}

  $( The subset class is a relationship.  (Contributed by Scott Fenton,
     31-Mar-2012.) $)
  relsset $p |- Rel SSet $=
    ( csset wrel cvv cxp wss cep cdif ctxp df-sset difss eqsstri df-rel mpbir
    crn ) ABACCDZEAOFCFGHNZGOIOPJKALM $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A x y $.  $d B x y $.
    brsset.1 $e |- B e. _V $.
    $( For sets, the ` SSet ` binary relationship is equivalent to the subset
       relationship.  (Contributed by Scott Fenton, 31-Mar-2012.) $)
    brsset $p |- ( A SSet B <-> A C_ B ) $=
      ( vx vy csset wbr cvv wcel wss relsset brrelexi cv cep cdif wn wex wa vex
      mpbiran ssex breq1 sseq1 cop ctxp crn wel wi wal opex elrn brtxp epel brv
      brdif epelcNEW xchbinx anbi12i bitri exbii exanali 3bitrri con1bii eleq2i
      cxp df-br df-sset opelvv eldif 3bitri dfss2 3bitr4i vtoclbg pm5.21nii ) A
      BFGZAHIABJZABFKLABCUADMZBFGZVQBJZVOVPDAHVQABFUBVQABUCVQBUDZNHNOZUEZUFZIZP
      ZEDUGZEMZBIZUHEUIZVRVSWIWDWDWGVTWBGZEQWFWHPZRZEQWIPEVTWBVQBUJUKWJWLEWJWGV
      QNGZWGBWAGZRWLNWAWGVQBESDSZCULWMWFWNWKEDUMWNWGBNGZWHWNWGBHGWPPWGBUNWGBHNU
      OTWGBCUPUQURUSUTWFWHEVAVBVCVRVTFIVTHHVEZWCOZIZWEVQBFVFFWRVTVGVDWSVTWQIWEV
      QBWOVHVTWQWCVITVJEVQBVKVLVMVN $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y z $.
    $( ` _I ` is equal to ` SSet ` and its converse.  (Contributed by Scott
       Fenton, 31-Mar-2012.) $)
    idsset $p |- _I = ( SSet i^i `' SSet ) $=
      ( vy vz cid csset ccnv cin reli wrel relsset relin1 ax-mp weq cv wss eqss
      wa wbr vex brsset bitri ideq brin brcnv anbi12i 3bitr4i eqbrriv ) ABCDDEZ
      FZGDHUHHIDUGJKABLAMZBMZNZUJUINZPZUIUJCQUIUJUHQZUIUJOUIUJBRZUAUNUIUJDQZUIU
      JUGQZPUMUIUJDUGUBUPUKUQULUIUJUOSUQUJUIDQULUIUJDARZUOUCUJUIURSTUDTUEUF $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    eltrans.1 $e |- A e. _V $.
    $( Membership in the class of all transitive sets.  (Contributed by Scott
       Fenton, 31-Mar-2012.) $)
    eltrans $p |- ( A e. Trans <-> Tr A ) $=
      ( ctrans wcel cvv cep ccom cdif crn wtr df-trans eleq2i dftr6 bitr4i ) AC
      DAEFFGFHIHZDAJCOAKLABMN $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y $.

    $( A quantifier-free definition of ` On ` .  (Contributed by Scott Fenton,
       5-Apr-2012.) $)
    dfon3 $p |- On =
     ( _V \ ran ( ( SSet i^i ( Trans X. _V ) ) \ ( _I u. _E ) ) ) $=
      ( vy vx con0 cv wa cvv csset ctrans cid cep cdif wcel wbr wex vex anbi12i
      wn bitri wo anbi1i wpss wtr wel wi wal cab cxp cin cun crn dfon2 wb abeq1
      wceq elrn wss weq brin brxp mpbiran2 eltrans ioran brun ideq epel orbi12i
      brsset brdif dfpss2 an32 anass 3bitr4i exbii exanali 3bitri con2bii eldif
      xchnxbir mpbiran bitr4i mpgbir eqtri ) CADZBDZUAZWCUBZEZABUCZUDAUEZBUFZFG
      HFUGZUHZIJUIZKZUJZKZBAUKWJWPUNWIWDWPLZULBWIBWPUMWIWDWOLZQZWQWRWIWRWCWDWNM
      ZANWGWHQZEZANWIQAWDWNBOZUOWTXBAWCWDWLMZWCWDWMMZQZEWCWDUPZWFEZABUQZQZXAEZE
      ZWTXBXDXHXFXKXDWCWDGMZWCWDWKMZEXHWCWDGWKURXMXGXNWFWCWDXCVGXNWCHLZWFXNXOWD
      FLZXCWCWDHFXCUSUTWCAOVARPRXIWHSZXKXEXIWHVBXEWCWDIMZWCWDJMZSXQWCWDIJVCXRXI
      XSWHWCWDXCVDABVEVFRVRPWCWDWLWMVHXBXHXJEZXAEXLWGXTXAWGXGXJEZWFEXTWEYAWFWCW
      DVITXGXJWFVJRTXHXJXAVKRVLVMWGWHAVNVOVPWQXPWSXCWDFWOVQVSVTWAWB $.
      $( [11-Apr-2012] $) $( [5-Apr-2012] $)
  $}

  $( Another quantifier-free definition of ` On ` .  (Contributed by Scott
     Fenton, 4-May-2014.) $)
  dfon4 $p |- On = ( _V \ ( ( SSet \ ( _I u. _E ) ) " Trans ) ) $=
    ( con0 cvv csset ctrans cxp cin cid cep cun cdif crn cima dfon3 cres df-ima
    df-res incom indifcom eqtri indif2 3eqtri rneqi difeq2i eqtr4i ) ABCDBEZFGH
    IZJZKZJBCUFJZDLZJMUJUHBUJUIDNZKUHUIDOUKUGUKUIUEFZCUEUFJFZUGUIDPULUEUIFUMUIU
    EQUECUFRSCUEUFTUAUBSUCUD $.
    $( [3-May-2014] $)

  ${
    $d A x $.  $d B x $.  $d R x $.
    brtxpsd.1 $e |- A e. _V $.
    brtxpsd.2 $e |- B e. _V $.
    $( Expansion of a common form used in quantifier-free definitions.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    brtxpsd $p |- ( -. A ran ( ( _V (x) _E ) (++) ( R (x) _V ) ) B <->
       A. x ( x e. B <-> x R A ) ) $=
      ( cv wcel wbr wb wal cvv cep ctxp csymdif crn wn wex brv brtxp df-br opex
      cop elrn brsymdif vex mpbiran epelcNEW bitri bibi12i xchbinx exbii 3bitri
      mpbiran2 exnal bitr2i con1bii ) AGZCHZURBDIZJZAKZBCLMNZDLNZOZPZIZVGVAQZAR
      ZVBQVGBCUCZVFHURVJVEIZARVIBCVFUAAVJVEBCUBUDVKVHAVKURVJVCIZURVJVDIZJVAURVJ
      VCVDUEVLUSVMUTVLURCMIZUSVLURBLIVNURBSLMURBCAUFZEFTUGURCFUHUIVMUTURCLIURCS
      DLURBCVOEFTUNUJUKULUMVAAUOUPUQ $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A x $.  $d B x $.  $d S x $.
    brtxpsd2.1 $e |- A e. _V $.
    brtxpsd2.2 $e |- B e. _V $.
    brtxpsd2.3 $e |- R = ( C \ ran ( ( _V (x) _E ) (++) ( S (x) _V ) ) ) $.
    brtxpsd2.4 $e |- A C B $.
    $( Another common abbreviation for quantifier-free definitions.
       (Contributed by Scott Fenton, 21-Apr-2014.) $)
    brtxpsd2 $p |- ( A R B <-> A. x ( x e. B <-> x S A ) ) $=
      ( wbr cvv cep ctxp csymdif crn wn cv wcel bitri wb wal cdif breqi mpbiran
      wa brdif brtxpsd ) BCEKZBCLMNFLNOPZKQZARZCSULBFKUAAUBUIBCDKZUKJUIBCDUJUCZ
      KUMUKUFBCEUNIUDBCDUJUGTUEABCFGHUHT $.
      $( [21-Apr-2014] $)

    ${
      $d X x $.
      brtxpsd3.5 $e |- ( x e. X <-> x S A ) $.
      $( A third common abbreviation for quantifier-free definitions.
         (Contributed by Scott Fenton, 3-May-2014.) $)
      brtxpsd3 $p |- ( A R B <-> B = X ) $=
        ( cv wcel wb wal wbr wceq bibi2i albii dfcleq brtxpsd2 3bitr4ri ) AMZCN
        ZUDGNZOZAPUEUDBFQZOZAPCGRBCEQUGUIAUFUHUELSTACGUAABCDEFHIJKUBUC $.
        $( [3-May-2014] $)
    $}
  $}

  $( The ` Bigcup ` relationship is a relationship.  (Contributed by Scott
     Fenton, 11-Apr-2012.) $)
  relbigcup $p |- Rel Bigcup $=
    ( cbigcup wrel cvv cxp cep ctxp ccom csymdif crn cdif relxp ax-mp df-bigcup
    reldif releqi mpbir ) ABCCDZCEFEEGCFHIZJZBZQBTCCKQRNLASMOP $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    brbigcup.1 $e |- B e. _V $.
    $( Binary relationship over ` Bigcup ` .  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    brbigcup $p |- ( A Bigcup B <-> U. A = B ) $=
      ( vx vy vz cbigcup wbr cvv wcel cuni wceq relbigcup brrelexi eleq1 mpbiri
      cv cep vex wrex uniexb sylibr breq1 unieq cxp ccom df-bigcup mpbir2an wel
      eqeq1d brxp epel rexbii coep eluni2 3bitr4ri brtxpsd3 eqcom bitri vtoclbg
      pm5.21nii ) ABGHZAIJZAKZBLZABGMNVEVDIJZVCVEVFBIJZCVDBIOPAUAUBDQZBGHZVHKZB
      LZVBVEDAIVHABGUCVHALVJVDBVHAUDUJVIBVJLVKEVHBIIUEZGRRUFZVJDSZCUGVHBVLHVHIJ
      VGVNCVHBIICUKUHEQZFQRHZFVHTEFUIZFVHTVOVHVMHVOVJJVPVQFVHEFULUMFVOVHRESVNUN
      FVOVHUOUPUQBVJURUSUTVA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d x y z t $.
    $( ` Bigcup ` using maps-to notation.  (Contributed by Scott Fenton,
       16-Apr-2012.) $)
    dfbigcup2 $p |- Bigcup = ( x e. _V |-> U. x ) $=
      ( vy vz vt cbigcup cvv cuni cmpt relbigcup mptrel wceq wbr eqcom brbigcup
      cv vex wcel wa weq eleq1 unieq eqeq2d anbi12d biantrur syl6bbr eqeq1 brab
      df-mpt 3bitr4i eqbrriv ) BCEAFAOZGZHZIAFULJBOZGZCOZKUPUOKZUNUPELUNUPUMLUO
      UPMUNUPCPZNUKFQZDOZULKZRZUTUOKZUQADUNUPUMBPZURABSZVBUNFQZVCRVCVEUSVFVAVCU
      KUNFTVEULUOUTUKUNUAUBUCVFVCVDUDUEUTUPUOUFADFULUHUGUIUJ $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` maps the universe onto itself.  (Contributed by Scott Fenton,
       16-Apr-2012.) $)
    fobigcup $p |- Bigcup : _V -onto-> _V $=
      ( vx vy cvv cbigcup wfo wfn crn wceq cuni wcel wral uniexg rgen dfbigcup2
      cv mptfng mpbi wrex cab rnmpt vex unisn eqcomi unieq eqeq2d rcla4ev mp2an
      csn snex 2th abbi2i eqtr4i df-fo mpbir2an ) CCDEDCFZDGZCHAOZIZCJZACKUOUSA
      CUQCLMACURDANZPQUPBOZURHZACRZBSCABCURDUTTVCBCVACJVCBUAZVAUHZCJVAVEIZHZVCV
      AUIVFVAVAVDUBUCVBVGAVECUQVEHURVFVAUQVEUDUEUFUGUJUKULCCDUMUN $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` is a function over the universal class.  (Contributed by
       Scott Fenton, 11-Apr-2012.) $)
    fnbigcup $p |- Bigcup Fn _V $=
      ( cvv cbigcup wfo wfn fobigcup fofn ax-mp ) AABCBADEAABFG $.
      $( [11-Apr-2012] $)
  $}

  ${
    fvbigcup.1 $e |- A e. _V $.
    $( For sets, ` Bigcup ` yields union.  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    fvbigcup $p |- ( Bigcup ` A ) = U. A $=
      ( cbigcup cfv cuni wceq wbr eqid uniex brbigcup mpbir cvv wfn wb fnbigcup
      wcel fnbrfvb mp2an ) ACDAEZFZASCGZUASSFSHASABIZJKCLMALPTUANOBLASCUBQRK $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix.1 $e |- A e. _V $.
    $( Membership in the fixpoints of a class.  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    elfix $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cid cin cdm cv wbr wex df-fix eleq2i eldm wceq bitri breq2
      wa brin vex ideq anbi2i exbii biimparc exlimiv eqeq2 anbi12d cla4ev mpan2
      eqid impbii 3bitri ) ABEZFABGHZIZFADJZUOKZDLZAABKZUNUPABMNDAUOCOUSAUQBKZA
      UQPZSZDLZUTURVCDURVAAUQGKZSVCAUQBGTVEVBVAAUQDUAUBUCQUDVDUTVCUTDVBUTVAAUQA
      BRUEUFUTAAPZVDAUKVCUTVFSDACUQAPVAUTVBVFUQAABRUQAAUGUHUIUJULQUM $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix2.1 $e |- Rel R $.
    $( Alternative membership in the fixpoint of a class.  (Contributed by
       Scott Fenton, 11-Apr-2012.) $)
    elfix2 $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cvv wbr elex brrelexi cv eleq1 wceq wb breq12 anidms elfix
      vex vtoclbg pm5.21nii ) ABEZFZAGFAABHZAUAIAABCJDKZUAFUDUDBHZUBUCDAGUDAUAL
      UDAMUEUCNUDAUDABOPUDBDRQST $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.
    $( The fixpoints of a class in terms of its range.  (Contributed by Scott
       Fenton, 16-Apr-2012.) $)
    dffix2 $p |- Fix A = ran ( A i^i _I ) $=
      ( vx vy cfix cid cin crn cv wbr weq wa wex wcel equid vex equequ1 anbi12d
      breq1 cla4ev bitri mpan2 biimpac impbii elfix elrn brin ideq anbi2i exbii
      exlimiv 3bitr4i eqriv ) BADZAEFZGZBHZUPAIZCHZUPAIZCBJZKZCLZUPUMMUPUOMZUQV
      BUQBBJZVBBNVAUQVDKCUPBOZUTUSUQUTVDURUPUPARZCBBPQSUAVAUQCUTUSUQVFUBUJUCUPA
      VEUDVCURUPUNIZCLVBCUPUNVEUEVGVACVGUSURUPEIZKVAURUPAEUFVHUTUSURUPVEUGUHTUI
      TUKUL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoints of a class are a subset of its domain.  (Contributed by
     Scott Fenton, 16-Apr-2012.) $)
  fixssdm $p |- Fix A C_ dom A $=
    ( cfix cid cin cdm df-fix wss inss1 dmss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  $( The fixpoints of a class are a subset of its range.  (Contributed by Scott
     Fenton, 16-Apr-2012.) $)
  fixssrn $p |- Fix A C_ ran A $=
    ( cfix cid cin crn dffix2 wss inss1 rnss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    $( The fixpoints of a class are the same as those of its converse.
       (Contributed by Scott Fenton, 16-Apr-2012.) $)
    fixcnv $p |- Fix A = Fix `' A $=
      ( vx cfix ccnv cv wbr wcel vex brcnv elfix 3bitr4ri eqriv ) BACZADZCZBEZP
      NFPPAFPOGPMGPPABHZQIPNQJPAQJKL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoint operator distributes over union.  (Contributed by Scott
     Fenton, 16-Apr-2012.) $)
  fixun $p |- Fix ( A u. B ) = ( Fix A u. Fix B ) $=
    ( cun cid cin cdm cfix indir dmeqi dmun eqtri df-fix uneq12i 3eqtr4i ) ABCZ
    DEZFZADEZFZBDEZFZCZOGAGZBGZCQRTCZFUBPUEABDHIRTJKOLUCSUDUAALBLMN $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    ellimits.1 $e |- A e. _V $.
    $( Membership in the class of all limit ordinals.  (Contributed by Scott
       Fenton, 11-Apr-2012.) $)
    ellimits $p |- ( A e. Limits <-> Lim A ) $=
      ( con0 cbigcup cfix cin wcel c0 csn wn wa word cuni wceq wne climits wlim
      3bitri anbi12i bitri elin elon elfix brbigcup eqcom elsnc necon3bbii cdif
      wbr df-limits eleq2i eldif w3a df-lim df-3an an32 3bitr4i ) ACDEZFZGZAHIZ
      GZJZKZALZAAMZNZKZAHOZKZAPGZAQZUTVHVCVIUTACGZAURGZKVHACURUAVMVEVNVGABUBVNA
      ADUIVFANVGADBUCAABUDVFAUERSTVBAHAHBUFUGSVKAUSVAUHZGVDPVOAUJUKAUSVAULTVLVE
      VIVGUMVEVIKVGKVJAUNVEVIVGUOVEVIVGUPRUQ $.
      $( [11-Apr-2012] $)
  $}

  $( The class of all limit ordinals is a subclass of the class of all
     ordinals.  (Contributed by Scott Fenton, 11-Apr-2012.) $)
  limitssson $p |- Limits C_ On $=
    ( vx climits con0 cv wlim word wcel limord vex ellimits elon 3imtr4i ssriv
    ) ABCADZENFNBGNCGNHNAIZJNOKLM $.
    $( [11-Apr-2012] $)

  ${
    $d x y $.
    $( A quantifier-free definition of ` om ` that does not depend on
       ~ ax-inf .  (Note: label was changed from ~ dfom5 to ~ dfom5b to prevent
       naming conflict.  NM 12-Feb-2013) (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    dfom5b $p |- om = ( On i^i |^| Limits ) $=
      ( vx vy cv word cab wlim wel wi wal cin con0 climits cint inab df-on wcel
      wa com df-int vex ellimits imbi1i albii abbii eqtri ineq12i 3eqtr4ri
      df-om ) ACDZAEZBCZFZABGZHZBIZAEZJUIUOQAEKLMZJRUIUOANKUJUQUPAOUQUKLPZUMHZB
      IZAEUPABLSUTUOAUSUNBURULUMUKBTUAUBUCUDUEUFABUHUG $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d F x y z w t a b c d $.
    elfuns.1 $e |- F e. _V $.
    $( Membership in the class of all functions.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    elfuns $p |- ( F e. Funs <-> Fun F ) $=
      ( vw vy vz va vb vd cv c1st cvv c2nd wbr wn wa wcel weq wi wb wex vt wrel
      vx cid cdif ccom ctxp wral cop wal cfuns wfun wceq breq2 notbid breq1 cfv
      vex op1st wfn wfo fo1st fofn ax-mp opex fnbrfvb mp2an mpbi mpbiran syl6bb
      vc brtxp rcla42v brco fo2nd fofun op2nd funbreq brv brdif xchbinx anbi12i
      exbii exanali 3bitri con2bii equid equequ2 equequ1 imbi12d a4v mpi eqcomd
      ideq sylbir syl6com alrimiv alrimivv elrel ee4anv opeq1 eleq1d anbi2d w3a
      ex opeq12 3adant3 3adant2 anbi12d eqeq12 3adant1 cla43gv mp3an com12 eqtr
      syl6bi syl8 com3l imbi2i imnan bitr2i syl6ibr eleq1 bi2anan9 breq12 bitri
      ancoms imbi2d mpbiri exlimivv a1i syl2and pm2.43d com23 ralrimdvv impbid2
      cep wrex bitr4i 3bitr4i pm5.32i cxp cpw ccnv cfix eldif elfix coep dfrex2
      coepr rexbii rexnal anbi2i df-funs eleq2i wss df-rel elpw anbi1i dffun4 )
      AUBZUAIZCIZJKUDUEZLUFZUGZMZNZUAAUHZCAUHZOZUVAUCIZDIZUIZAPZUVLEIZUIZAPZOZD
      EQZRZEUJZDUJUCUJZOAUKPZAULUVAUVJUWCUVAUVJUWCUVJUWBUCDUVJUWAEUVSUVJUVQUVMU
      VEMZNZUVTUVHUWFUVBUVNUVFMZNCUAUVNUVQAAUVCUVNUMUVGUWGUVCUVNUVBUVFUNUOUVBUV
      QUMZUWGUWEUWHUWGUVQUVNUVFMZUWEUVBUVQUVNUVFUPUWIUVQUVLJMZUWEUVQJUQUVLUMZUW
      JUVLUVPUCURZUSJKUTZUVQKPZUWKUWJSKKJVAZUWMVBKKJVCVDZUVLUVPVEZKUVQUVLJUWLVF
      VGVHJUVEUVQUVLUVMUWQUWLDURZVLVIVJUOVMUWFECQZCDQZRZCUJZUVTUWEUXBUWEUVQUVCL
      MZUVCUVMUVDMZOZCTUWSUWTNZOZCTUXBNCUVQUVMUVDLUWQUWRVNUXEUXGCUXCUWSUXDUXFLU
      LZUVQUVPLMZUXCUWSSKKLVAZUXHVOKKLVPVDZUVQLUQUVPUMZUXIUVLUVPUWLEURZVQLKUTZU
      WNUXLUXISUXJUXNVOKKLVCVDZUWQKUVQUVPLUXMVFVGVHUVQUVPUVCLUWQUXMCURZVRVGUXDU
      VCUVMUDMZUWTUXDUVCUVMKMUXQNUVCUVMVSUVCUVMKUDVTVIUVCUVMUWRWNWAWBWCUWSUWTCW
      DWEWFUXBUVPUVMUXBEEQZEDQZEWGUXAUXRUXSRCECEQUWSUXRUWTUXSCEEWHCEDWIWJWKWLWM
      WOWPWQWRUVAUWCUVHCUAAAUVAUVCAPZUVBAPZOZUWCUVHUVAUYBUWCUVHRZUVAUXTUVCFIZGI
      ZUIZUMZGTFTZUYAUVBVKIZHIZUIZUMZHTVKTZUYBUYCRZUVAUXTUYHFGUVCAWSXEUVAUYAUYM
      VKHUVBAWSXEUYHUYMOZUYNRUVAUYOUYGUYLOZHTVKTZGTFTUYNUYGUYLFGVKHWTUYQUYNFGUY
      PUYNVKHUYPUYNUYFAPZUYKAPZOZUWCVKFQZHCQZCGQZNZOZCTZOZNZRZRUYTUWCVUAVUBVUCR
      ZCUJZRZVUHVUAUYTUWCVUKVUAUYTUWCGHQZVUKVUAUYTUYRUYDUYJUIZAPZOZUWCVUMRVUAUY
      SVUOUYRVUAUYKVUNAUYIUYDUYJXAXBXCUWCVUPVUMUYDKPUYEKPUYJKPUWCVUPVUMRZRFURZG
      URZHURZUWAVUQUCDEUYDUYEUYJKKKUCFQZDGQZEHQZXDZUVSVUPUVTVUMVVDUVOUYRUVRVUOV
      VAVVBUVOUYRSVVCVVAVVBOUVNUYFAUVLUVMUYDUYEXFXBXGVVAVVCUVRVUOSVVBVVAVVCOUVQ
      VUNAUVLUVPUYDUYJXFXBXHXIVVBVVCUVTVUMSVVAUVMUYEUVPUYJXJXKWJXLXMXNXPVUMVUJC
      VUMVUBVUCVUMVUBOUYEUVCUYEUYJUVCXOWMXEWQXQXRVULVUAVUFNZRVUHVUKVVEVUAVUFVUK
      VUBVUCCWDWFXSVUAVUFXTYAYBUYPUYBUYTUYCVUIUYGUXTUYRUYLUYAUYSUVCUYFAYCUVBUYK
      AYCYDUYPUVHVUHUWCUYLUYGUVHVUHSUYLUYGOZUVGVUGVVFUVGUYKUYFUVFMZVUGUVBUYKUVC
      UYFUVFYEVVGUYKUYDJMZUYKUYEUVEMZOVUGJUVEUYKUYDUYEUYIUYJVEZVURVUSVLVVHVUAVV
      IVUFJULZUYKUYIJMZVVHVUASUWOVVKVBKKJVPVDUYKJUQUYIUMZVVLUYIUYJVKURZUSUWMUYK
      KPZVVMVVLSUWPVVJKUYKUYIJVVNVFVGVHUYKUYIUYDJVVJVVNVURVRVGVVIUYKUVCLMZUVCUY
      EUVDMZOZCTVUFCUYKUYEUVDLVVJVUSVNVVRVUECVVPVUBVVQVUDUXHUYKUYJLMZVVPVUBSUXK
      UYKLUQUYJUMZVVSUYIUYJVVNVUTVQUXNVVOVVTVVSSUXOVVJKUYKUYJLVUTVFVGVHUYKUYJUV
      CLVVJVUTUXPVRVGVVQUVCUYEUDMZVUCVVQUVCUYEKMVWANUVCUYEVSUVCUYEKUDVTVIUVCUYE
      VUSWNWAWBWCYFWBYFVJUOYGYHWJYIYJYJWOYKYLYMYNYOYPUUAAKKUUBZUUCZYQUVFYQUUDUF
      ZUFZUUEZUEZPZAVWCPZUVJOZUWDUVKVWHVWIAVWFPZNZOVWJAVWCVWFUUFUVJVWLVWIVWKUVJ
      VWKAAVWEMAUVCVWDMZCAYRZUVJNZAVWEBUUGCAAVWDBBUUHVWNUVINZCAYRVWOVWMVWPCAVWM
      UVGUAAYRVWPUAAUVCUVFBUXPUUJUVGUAAUUIYFUUKUVICAUULYFWEWFUUMYSUKVWGAUUNUUOU
      VAVWIUVJUVAAVWBUUPVWIAUUQAVWBBUURYSUUSYTUCDEAUUTYT $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d F f $.
    $( Closed form of ~ elfuns .  (Contributed by Scott Fenton, 2-May-2014.) $)
    elfunsg $p |- ( F e. V -> ( F e. Funs <-> Fun F ) ) $=
      ( vf cv cfuns wcel wfun eleq1 funeq vex elfuns vtoclbg ) CDZEFMGAEFAGCABM
      AEHMAIMCJKL $.
      $( [2-May-2014] $)
  $}

  ${
    $d A x $.  $d B x $.
    brsingle.1 $e |- A e. _V $.
    brsingle.2 $e |- B e. _V $.
    $( The binary relationship form of the singleton function.  (Contributed by
       Scott Fenton, 04-Apr-2014. ) $)
    brsingle $p |- ( A Singleton B <-> B = { A } ) $=
      ( vx cvv cxp csingle cid csn df-singleton wcel brxp mpbir2an cv wceq elsn
      wbr ideq bitr4i brtxpsd3 ) EABFFGZHIAJZCDKABUBRAFLBFLCDABFFDMNEOZUCLUDAPU
      DAIREAQUDACSTUA $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)

  $}

  ${
    $d A x y z $.
    $( Membership in the class of all singletons.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    elsingles $p |- ( A e. Singletons <-> E. x A = { x } ) $=
      ( vy csingles wcel cvv csn wceq wex elex snex eleq1 mpbiri exlimiv exbidv
      cv eqeq1 csingle crn vex wbr df-singles eleq2i elrn brsingle exbii 3bitri
      vtoclbg pm5.21nii ) BDEZBFEZBAPZGZHZAIZBDJUNUKAUNUKUMFEULKBUMFLMNCPZDEZUP
      UMHZAIZUJUOCBFUPBDLUPBHURUNAUPBUMQOUQUPRSZEULUPRUAZAIUSDUTUPUBUCAUPRCTZUD
      VAURAULUPATVBUEUFUGUHUI $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d x y z $.
    $( The singleton relationship is a function over the universe.
       (Contributed by Scott Fenton, 04-Apr-2014.) $)
    fnsingle $p |- Singleton Fn _V $=
      ( vx vy vz csingle cvv wfn wfun cdm wceq wrel cv wbr wa weq wal mpbir vex
      ctxp brsingle mpbir2an wi cxp cep cid csymdif crn cdif difss df-singleton
      wss df-rel releqi csn eqtr3 syl2anb ax-gen gen2 dffun2 wcel eqv eqid snex
      wex breq2 cla4ev ax-mp eldm mpgbir df-fn ) DEFDGZDHZEIZVJDJZAKZBKZDLZVNCK
      ZDLZMBCNZUAZCOZBOAOVMEEUBZEUCRUDERUEUFZUGZJZWEWDWBUJWBWCUHWDUKPDWDUIULPWA
      ABVTCVPVOVNUMZIVQWFIVSVRVNVOAQZBQSVNVQWGCQSVOVQWFUNUOUPUQABCDURTVLVNVKUSZ
      AAVKUTWHVPBVCZVNWFDLZWIWJWFWFIWFVAVNWFWGVNVBZSPVPWJBWFWKVOWFVNDVDVEVFBVND
      WGVGPVHDEVIT $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d A x $.
    $( The value of the singleton function.  (Contributed by Scott Fenton,
       04-Apr-2014.) $)
    fvsingle $p |- ( A e. V -> ( Singleton ` A ) = { A } ) $=
      ( vx cv csingle cfv csn wceq fveq2 sneq eqeq12d wbr eqid snexALT brsingle
      vex mpbir cvv wfn wcel wb fnsingle fnbrfvb mp2an vtoclg ) CDZEFZUFGZHZAEF
      ZAGZHCABUFAHUGUJUHUKUFAEIUFAJKUIUFUHELZULUHUHHUHMUFUHCPZUFNZOQERSUFRTUIUL
      UAUBUMRUFUHEUNUCUDQUE $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d x y $.
    $( Alternate definition of the class of all singletons.  (Contributed by
       Scott Fenton, 20-Nov-2013.) $)
    dfsingles2 $p |- Singletons = { x | E. y x = { y } } $=
      ( cv csn wceq wex csingles elsingles abbi2i ) ACZBCDEBFAGBJHI $.
      $( [19-Apr-2014] $) $( [20-Nov-2013] $)
  $}

  ${
    $d A x $.
    snelsingles.1 $e |- A e. _V $.
    $( A singleton is a member of the class of all singletons.  (Contributed by
       Scott Fenton, 19-Feb-2013.) $)
    snelsingles $p |- { A } e. Singletons $=
      ( vx csn csingles wcel cv wceq wex cvv isset eqcom exbii bitri mpbi eximi
      sneq ax-mp elsingles mpbir ) ADZEFUACGZDHZCIZAUBHZCIZUDAJFZUFBUGUBAHZCIUF
      CAKUHUECUBALMNOUEUCCAUBQPRCUAST $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d x y z w $.  $d ph y z w $.
    $( A definiton of iota using minimal quantifiers.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    dfiota3 $p |- ( iota x ph ) = U. U. ( { { x | ph } } i^i Singletons ) $=
      ( vy vz vw cab cv csn wceq cuni csingles cin wex wcel bitri ceqsexv exbii
      wa weq unieqi cio df-iota abeq1 exdistr snexALT eqeq1 eleq2 anbi12d ancom
      wb wel elsn equcom eqcom anbi12i syl6bb an13 bitr3i vex sneq eqeq2d excom
      3bitr3i eluniab 3bitr4i mpgbir df-sn dfsingles2 ineq12i inab 19.42v abbii
      bicomi 3eqtri eqtr4i eqtri ) ABUAABFZCGZHZIZCFZJVQHZKLZJZJABCUBWAWDWADGZV
      QIZWEEGZHZIZRZEMZDFZJZWDWAWMIVTVRWMNZUJCVTCWMUCCDUKZWJRZEMDMZWOWKRDMVTWNW
      OWJDEUDECSZVQWHIZRZEMWPDMZEMVTWQWTXAEWTWIWFWORZRZDMXAXBWTDWHWGUEWIXBWHVQI
      ZVRWHNZRZWTWIWFXDWOXEWEWHVQUFWEWHVRUGUHXFXEXDRWTXDXEUIXEWRXDWSXECESWRCWGU
      LCEUMOWHVQUNUOOUPPXCWPDWIWFWOUQQURQWSVTEVRCUSWRWHVSVQWGVRUTVAPWPEDVBVCWKD
      VRVDVEVFWCWLWCWFDFZWIEMZDFZLWFXHRZDFWLWBXGKXIDVQVGDEVHVIWFXHDVJXJWKDWKXJW
      FWIEVKVMVLVNTVOTVP $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d F x $.  $d A x $.
    $( Another quantifier free definition of function value.  (Contributed by
       Scott Fenton, 19-Feb-2013.) $)
    dffv4 $p |- ( F ` A ) = U. U. ( { ( F " { A } ) } i^i Singletons ) $=
      ( vx cfv cv csn cima wcel cio cab csingles cuni dffv3 dfiota3 abid2 sneqi
      cin ineq1i unieqi 3eqtri ) ABDCEBAFGZHZCIUBCJZFZKQZLZLUAFZKQZLZLCABMUBCNU
      FUIUEUHUDUGKUCUACUAOPRSST $.
      $( [19-Feb-2013] $)
  $}

  $( Express union of singleton in terms of ` if ` .  (Contributed by Scott
     Fenton, 27-Mar-2014.) $)
  unisnif $p |- U. { A } = if ( A e. _V , A , (/) ) $=
    ( cvv wcel c0 cif csn cuni wceq iftrue unisng eqtr4d wn snprc biimpi unieqd
    iffalse uni0 syl6eq pm2.61i eqcomi ) ABCZADEZAFZGZUAUBUDHUAUBAUDUAADIABJKUA
    LZUBDUDUAADPUEUDDGDUEUCDUEUCDHAMNOQRKST $.
    $( [19-Apr-2014] $) $( [27-Mar-2014] $)

  ${
    $d A x y $.  $d B x y $.  $d R x y $.
    brimage.1 $e |- A e. _V $.
    brimage.2 $e |- B e. _V $.
    $( Binary relationship form of the Image functor.  (Contributed by Scott
       Fenton, 04-Apr-2014.) $)
    brimage $p |- ( A Image R B <-> B = ( R " A ) ) $=
      ( vx vy cvv cxp cimage cep ccnv ccom cima df-image wbr wcel cv wrex vex
      brxp mpbir2an brcnv rexbii coep elima 3bitr4ri brtxpsd3 ) FABHHIZCJKCLZMZ
      CANZDECOABUIPAHQBHQDEABHHEUAUBFRZGRZUJPZGASUNUMCPZGASUMAUKPUMULQUOUPGAUMU
      NCFTZGTUCUDGUMAUJUQDUEGUMCAUQUFUGUH $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d R x y $.  $d A x y $.  $d B x y $.
    $( Closed form of ~ brimage .  (Contributed by Scott Fenton,
       04-Apr-2014.) $)
    brimageg $p |- ( ( A e. V /\ B e. W ) ->
               ( A Image R B <-> B = ( R " A ) ) ) $=
      ( vx vy cv cimage wbr cima wb breq1 imaeq2 eqeq2d bibi12d breq2 eqeq1 vex
      wceq brimage vtocl2g ) FHZGHZCIZJZUDCUCKZTZLAUDUEJZUDCAKZTZLABUEJZBUJTZLF
      GABDEUCATZUFUIUHUKUCAUDUEMUNUGUJUDUCACNOPUDBTUIULUKUMUDBAUEQUDBUJRPUCUDCF
      SGSUAUB $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d A x y z t w $.
    $( ` Image A ` is a function.  (Contributed by Scott Fenton,
       27-Mar-2014.) $)
    funimage $p |- Fun Image A $=
      ( vx vy vz cimage wfun wrel cv wbr wa weq wal cvv cep ctxp mpbir wceq vex
      wi brimage cxp ccnv ccom csymdif crn cdif wss df-rel df-image releqi cima
      difss eqtr3 syl2anb gen2 ax-gen dffun2 mpbir2an ) AEZFUSGZBHZCHZUSIZVADHZ
      USIZJCDKZSZDLCLZBLUTMMUAZMNONAUBUCMOUDUEZUFZGZVLVKVIUGVIVJULVKUHPUSVKAUIU
      JPVHBVGCDVCVBAVAUKZQVDVMQVFVEVAVBABRZCRTVAVDAVNDRTVBVDVMUMUNUOUPBCDUSUQUR
      $.
      $( [19-Apr-2014] $) $( [27-Mar-2014] $)
  $}

  ${
    $d R x y $.
    $( ` Image R ` is a function over the set-like portion of ` R ` .
       (Contributed by Scott Fenton, 04-Apr-2014.) $)
    fnimage $p |- Image R Fn { x | ( R " x ) e. _V } $=
      ( cimage cima cvv wcel cab wfn wfun cdm wceq funimage wbr wex vex brimage
      vy cv eleq1 mpbii sylbi exlimiv eqid wb brimageg mpbiri breq2 cla4egv mpd
      mpan impbii eldm weq imaeq2 eleq1d elab 3bitr4i eqriv df-fn mpbir2an ) BC
      ZBARZDZEFZAGZHVAIVAJZVEKBLQVFVEQRZVBVAMZANZBVGDZEFZVGVFFVGVEFVIVKVHVKAVHV
      BVJKZVKVGVBBQOZAOZPVLVBEFVKVNVBVJESTUAUBVKVGVJVAMZVIVKVOVJVJKZVJUCVGEFVKV
      OVPUDVMVGVJBEEUEUJUFVHVOAVJEVBVJVGVAUGUHUIUKAVGVAVMULVDVKAVGVMAQUMVCVJEVB
      VGBUNUOUPUQURVAVEUSUT $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d R x y z $.
    $( The image functor in maps-to notation.  (Contributed by Scott Fenton,
       04-Apr-2014.) $)
    imageval $p |- Image R = ( x e. _V |-> ( R " x ) ) $=
      ( vy vz cimage cvv cv cima wfun wbr wcel cdm vex breldm wfn wceq syl6eleq
      ax-mp wb syl5bb cmpt wrel funimage funrel mptrel cab fndm crab eqid dmmpt
      fnimage rabab eqtri weq imaeq2 eleq1d elab brimage cfv fvmptg mpan eqeq1d
      eqcom funmpt df-fn biimpri fnbrfvb sylancr bitr3d sylbi pm5.21nii eqbrriv
      mpbir2an ) CDBEZAFBAGZHZUAZVNIVNUBBUCVNUDRAFVPUECGZDGZVNJZVRVPFKZAUFZKZVR
      VSVQJZVTVRVNLZWBVRVSVNCMZNVNWBOWEWBPABUKWBVNUGRQWDVRVQLZWBVRVSVQWFNWGWAAF
      UHWBAFVPVQVQUIZUJWAAULUMZQWCBVRHZFKZVTWDSWAWKAVRWFACUNVPWJFVOVRBUOZUPUQZV
      TVSWJPZWKWDVRVSBWFDMZURWNWJVSPZWKWDVSWJVCWKVRVQUSZVSPZWPWDWKWQWJVSVRFKWKW
      QWJPWFAVRVPWJFFVQWLWHUTVAVBWKVQWBOZWCWRWDSWSVQIWGWBPAFVPVDWIVQWBVEVMWCWKW
      MVFWBVRVSVQWOVGVHVITTVJVKVL $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d R x $.  $d A x $.
    $( The value of the image functor.  (Contributed by Scott Fenton,
       04-Apr-2014.) $)
    fvimage $p |- ( ( A e. V /\ ( R " A ) e. W ) ->
             ( Image R ` A ) = ( R " A ) ) $=
      ( vx wcel cvv cima cimage cfv wceq elex cv imaeq2 imageval fvmptg sylan )
      ACFAGFBAHZDFABIZJRKACLEABEMZHRGDSTABNEBOPQ $.
      $( [19-Apr-2014] $) $( [4-Apr-2014] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.
    brcart.1 $e |- A e. _V $.
    brcart.2 $e |- B e. _V $.
    brcart.3 $e |- C e. _V $.
    $( Binary relationship form of the cartesian product operator.
       (Contributed by Scott Fenton, 11-Apr-2014.) $)
    brcart $p |- ( <. A , B >. Cart C <-> C = ( A X. B ) ) $=
      ( vx vy vz cop cvv cxp ccart cep wbr wcel cv wex wa epelcNEW opex df-cart
      cpprod opelvv brxp mpbir2an wceq w3a 3anass anbi2i bitri 2exbii brpprod3b
      anbi12i vex elxp 3bitr4ri brtxpsd3 ) GABJZCKKLZKLZMNNUCZABLZABUAFUBUSCVAO
      USUTPCKPABDUDFUSCUTKFUEUFGQZHQZIQZJUGZVEANOZVFBNOZUHZIRHRVGVEAPZVFBPZSZSZ
      IRHRVDUSVBOVDVCPVJVNHIVJVGVHVISZSVNVGVHVIUIVOVMVGVHVKVIVLVEADTVFBETUNUJUK
      ULHINNVDABGUODEUMHIVDABUPUQUR $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)
  $}

  ${
    $d A x p $.  $d B x p $.
    brdomain.1 $e |- A e. _V $.
    brdomain.2 $e |- B e. _V $.
    $( The binary relationship form of the domain function.  (Contributed by
       Scott Fenton, 11-Apr-2014.) $)
    brdomain $p |- ( A Domain B <-> B = dom A ) $=
      ( c1st cvv cxp cres cimage wbr cima cdomain brimage df-domain breqi dfdm5
      wceq cdm eqeq2i 3bitr4i ) ABEFFGHZIZJBUAAKZQABLJBARZQABUACDMABLUBNOUDUCBA
      PST $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)

    $( The binary relationship form of the range function.  (Contributed by
       Scott Fenton, 11-Apr-2014.) $)
    brrange $p |- ( A Range B <-> B = ran A ) $=
      ( c2nd cvv cxp cres cimage wbr cima wceq crn brimage df-range breqi dfrn5
      crange eqeq2i 3bitr4i ) ABEFFGHZIZJBUAAKZLABRJBAMZLABUACDNABRUBOPUDUCBAQS
      T $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A a b $.  $d B a b $.
    $( Closed form of ~ brdomain .  (Contributed by Scott Fenton,
       2-May-2014.) $)
    brdomaing $p |- ( ( A e. V /\ B e. W ) ->
               ( A Domain B <-> B = dom A ) ) $=
      ( va vb cv cdomain wbr cdm wceq breq1 dmeq eqeq2d bibi12d breq2 eqeq1 vex
      wb brdomain vtocl2g ) EGZFGZHIZUCUBJZKZSAUCHIZUCAJZKZSABHIZBUHKZSEFABCDUB
      AKZUDUGUFUIUBAUCHLULUEUHUCUBAMNOUCBKUGUJUIUKUCBAHPUCBUHQOUBUCERFRTUA $.
      $( [2-May-2014] $)

    $( Closed form of ~ brrange .  (Contributed by Scott Fenton,
       3-May-2014.) $)
    brrangeg $p |- ( ( A e. V /\ B e. W ) ->
               ( A Range B <-> B = ran A ) ) $=
      ( va vb cv crange wbr crn wceq wb breq1 rneq eqeq2d bibi12d breq2 brrange
      eqeq1 vex vtocl2g ) EGZFGZHIZUCUBJZKZLAUCHIZUCAJZKZLABHIZBUHKZLEFABCDUBAK
      ZUDUGUFUIUBAUCHMULUEUHUCUBANOPUCBKUGUJUIUKUCBAHQUCBUHSPUBUCETFTRUA $.
      $( [3-May-2014] $)
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a p $.  $d A p $.  $d a q $.  $d A q $.
    $d a x $.  $d A x $.  $d a y $.  $d A y $.  $d B a $.  $d B b $.  $d b p $.
    $d B p $.  $d b q $.  $d B q $.  $d b x $.  $d B x $.  $d b y $.  $d B y $.
    $d C x $.  $d p q $.  $d p x $.  $d p y $.  $d q x $.  $d q y $.  $d x y $.
    brimg.1 $e |- A e. _V $.
    brimg.2 $e |- B e. _V $.
    brimg.3 $e |- C e. _V $.
    $( The binary relationship form of the ` Img ` function.  (Contributed by
       Scott Fenton, 12-Apr-2014.) $)
    brimg $p |- ( <. A , B >. Img C <-> C = ( A " B ) ) $=
      ( vx vy vp vq va vb c1st wbr wa wex exbii bitri 3bitri wcel cop c2nd ccom
      cvv cxp cres cimage ccart cima wceq cimg opex brco vex brcart anbi1i xpex
      breq1 ceqsexv brimage df-img breqi wrex df-rex elxp 19.41vv anass 3bitr2i
      2exbii excom brres df-br ancom elvv anbi2i 3bitr4ri 3bitr3i bitr2i anbi2d
      syl6bbr an12 19.42vv exrot3 weq exdistr eleq1 anbi1d opeq1 breq1d anbi12d
      cv br1steq syl6bb exbidv exancom opeq2 an32 3bitr4i eleq1d br2ndeq equcom
      elima rexbii eqriv eqeq2i ) ABUAZCUBMUCZMUDUDUEZUFZUFZUGZUHUCZNZCXJABUEZU
      IZUJZXFCUKNCABUIZUJXMXFGWKZUHNZXRCXKNZOZGPXRXNUJZXTOZGPZXPGXFCXKUHABULFUM
      YAYCGXSYBXTABXRDEGUNZUOUPQYDXNCXKNZXPXTYFGXNABDEUQZXRXNCXKURUSXNCXJYGFUTR
      SXFCUKXLVAVBXQXOCGXQXOHWKZXRXJNZHXNVCZIWKZJWKZUAZATZYMYKUAZXRXGNZOZJPZIBV
      CZXRXOTXRXQTZYJYHXNTZYIOZHPYHKWKZLWKZUAZUJZUUCATZUUDBTZOZYIOZOZLPZKPZHPZY
      SYIHXNVDUUBUUMHUUBUUFUUIOZLPKPZYIOUUOYIOZLPKPUUMUUAUUPYIKLYHABVEUPUUOYIKL
      VFUUQUUKKLUUFUUIYIVGVIVHQUUNUULHPZKPUUIUUEXRXGNZUUCYMUJZUUCUUDMNZOZJPIPZO
      ZOZLPZKPZYSUULHKVJUURUVFKUURUUKHPZLPUVFUUKHLVJUVHUVELUUJUVEHUUEUUCUUDULUU
      FYIUVDUUIUUFYIUUEXRXJNZUVDYHUUEXRXJURUVIUUSUUEXITZOUVDUUEXRXGXIYEVKUVJUVC
      UUSUUCUUDXINUVAUUCXHTZOZUVJUVCUUCUUDMXHLUNZVKUUCUUDXIVLUUTJPIPZUVAOUVAUVN
      OUVCUVLUVNUVAVMUUTUVAIJVFUVKUVNUVAIJUUCVNVOVPVQVOVRVTVSUSQRQUUIUUSOZUVBOZ
      JPIPZLPKPUUTUVOUVAOZOZJPIPZLPZKPZUVGYSUVQUVTKLUVPUVSIJUVOUUTUVAWAVIVIUVQU
      VEKLUVQUVOUVCOUVEUVOUVBIJWBUUIUUSUVCVGRVIUWBUVSLPZJPIPZKPUWCKPZJPIPZYSUWA
      UWDKUVSLIJWCQUWCKIJWCUWFYNUUHOZYMUUDUAZXRXGNZOZLIWDZOZLPZJPIPYNYKBTZOZYPO
      ZJPIPZYSUWEUWMIJUWEUUTUVRLPZOKPUWMUUTUVRKLWEUWRUWMKYMYKYLULZUUTUVRUWLLUUT
      UVOUWJUVAUWKUUTUUIUWGUUSUWIUUTUUGYNUUHUUCYMAWFWGUUTUUEUWHXRXGUUCYMUUDWHWI
      WJUUTUVAYMUUDMNUWKUUCYMUUDMURYKYLUUDIUNZJUNZUVMWLWMWJWNUSRVIUWMUWPIJUWMUW
      KUWJOLPUWPUWJUWKLWOUWJUWPLYKUWTUWKUWGUWOUWIYPUWKUUHUWNYNUUDYKBWFVSUWKUWHY
      OXRXGUUDYKYMWPWIWJUSRVIUWNYQOZJPIPUWNYROIPUWQYSUWNYQIJWEUWPUXBIJUWPYQUWNO
      UXBYNUWNYPWQYQUWNVMRVIYRIBVDWRSSVQSSHXRXJXNYEXBYTYKXRANZIBVCYSIXRABYEXBUX
      CYRIBJGWDZYNOZJPYKXRUAZATZYRUXCYNUXGJXRYEUXDYMUXFAYLXRYKWPWSUSYQUXEJYQYPY
      NOUXEYNYPVMYPUXDYNYPYOYHMNZYHXRUBNZOZHPYHYMUJZUXIOZHPZUXDHYOXRUBMYMYKULYE
      UMUXJUXLHUXHUXKUXIYMYKYHUWSUWTHUNWLUPQUXMYMXRUBNZGJWDUXDUXIUXNHYMUWSYHYMX
      RUBURUSYKYLXRUWTUXAYEWTGJXASSUPRQYKXRAVLVPXCRVPXDXEWR $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a x $.  $d A x $.  $d A y $.  $d B a $.
    $d B b $.  $d b x $.  $d B x $.  $d B y $.  $d C x $.  $d C y $.  $d x y $.
    $d x z $.  $d y z $.  $d a z $.  $d A z $.  $d b z $.  $d B z $.
    brapply.1 $e |- A e. _V $.
    brapply.2 $e |- B e. _V $.
    brapply.3 $e |- C e. _V $.
    $( The binary relationship form of the ` Apply ` function.  (Contributed by
       Scott Fenton, 12-Apr-2014.) $)
    brapply $p |- ( <. A , B >. Apply C <-> C = ( A ` B ) ) $=
      ( vx vy vz va vb csingles wceq wa wex wbr eqeq2d cbigcup cvv csingle cima
      csn cin cuni cop capply cfv snex inex1 unieq unieqd ceqsexv ccom cxp ctxp
      cv cep cres csymdif crn cdif cimg cid cpprod df-apply breqi opex brco vex
      w3a brpprod3a 3anrot ideq eqcom bitri brsingle biid 3anbi123i opeq1 opeq2
      2exbii ceqsex2v 3bitri anbi1i exbii breq1 brimg anbi12i wcel imaexg ax-mp
      sneq eqid brxp mpbir2an wel epel brres elin 3bitr4ri brtxpsd3 ineq1 uniex
      brbigcup dffv4 eqeq2i 3bitr4i ) GUPZABUBZUAZUBZLUCZMZCXHUDZUDZMZNZGOZCXLU
      DZUDZMZABUEZCUFPZCBAUGZMXPYAGXLXKLXJUHZUIXMXOXTCXMXNXSXHXLUJUKQULYCYBCRRU
      MZSSUNZSUQUOUQLURZSUOUSUTVAZTVBUMZVCTVDZUMZUMZUMZPYBXHYMPZXHCYFPZNZGOXRYB
      CUFYNVEVFGYBCYFYMABVGZFVHYQXQGYOXMYPXPYOYBHUPZYLPZYSXHYIPZNZHOYSXKMZXHYSL
      UCZMZNZHOXMHYBXHYIYLYRGVIZVHUUBUUFHYTUUCUUAUUEYTYBIUPZYKPZUUHYSYJPZNZIOUU
      HAXIUEZMZUUJNZIOZUUCIYBYSYJYKYRHVIZVHUUKUUNIUUIUUMUUJUUIUUHJUPZKUPZUEZMZA
      UUQVCPZBUURTPZVJZKOJOUUQAMZUURXIMZUUTVJZKOJOUUMJKVCTABUUHDEIVIVKUVCUVFJKU
      VCUVAUVBUUTVJUVFUUTUVAUVBVLUVAUVDUVBUVEUUTUUTUVAAUUQMUVDAUUQJVIVMAUUQVNVO
      BUUREKVIVPUUTVQVRVOWAUUTUUHAUURUEZMUUMJKAXIDBUHZUVDUUSUVGUUHUUQAUURVSQUVE
      UVGUULUUHUURXIAVTQWBWCWDWEUUOUULYSYJPZUULXHVBPZXHYSTPZNZGOZUUCUUJUVIIUULA
      XIVGZUUHUULYSYJWFULGUULYSTVBUVNUUPVHUVMXHXJMZYSXHUBZMZNZGOUUCUVLUVRGUVJUV
      OUVKUVQAXIXHDUVHUUGWGXHYSUUGUUPVPWHWEUVQUUCGXJASWIXJSWIDAXISWJWKUVOUVPXKY
      SXHXJWLQULVOWCWCIYSXHYGYIYHUUDUUPUUGYIWMYSXHYGPYSSWIXHSWIUUPUUGYSXHSSUUGW
      NWOUUHYSUQPZUUHLWIZNIHWPZUVTNUUHYSYHPUUHUUDWIUVSUWAUVTIHWQWDUUHYSUQLUUPWR
      UUHYSLWSWTXAWHWEUUEXMHXKYEUUCUUDXLXHYSXKLXBQULWCYPXHYSRPZYSCRPZNZHOYSXNMZ
      CYSUDZMZNZHOXPHXHCRRUUGFVHUWDUWHHUWBUWEUWCUWGUWBXNYSMUWEXHYSUUPXDXNYSVNVO
      UWCUWFCMUWGYSCFXDUWFCVNVOWHWEUWGXPHXNXHUUGXCUWEUWFXOCYSXNUJQULWCWHWEWCYDX
      TCBAXEXFXG $.
      $( [19-Apr-2014] $) $( [13-Apr-2014] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    brcup.1 $e |- A e. _V $.
    brcup.2 $e |- B e. _V $.
    brcup.3 $e |- C e. _V $.
    $( Binary relationship form of the ` Cup ` function.  (Contributed by Scott
       Fenton, 14-Apr-2014.) $)
    brcup $p |- ( <. A , B >. Cup C <-> C = ( A u. B ) ) $=
      ( vx vy cvv cxp c1st ccnv cep ccom c2nd wbr wcel wa wex bitri ccup df-cup
      cop cun opex opelvv brxp mpbir2an cv wo wceq vex brco ancom brcnv br1steq
      anbi1i exbii breq2 ceqsexv epelcNEW 3bitri br2ndeq brun 3bitr4ri brtxpsd3
      orbi12i elun ) GABUCZCIIJZIJZUAKLZMNZOLZMNZUDZABUDZABUEZFUBVICVKPVIVJQCIQ
      ABDUFFVICVJIFUGUHGUIZVIVMPZVSVIVOPZUJVSAQZVSBQZUJVSVIVPPVSVQQVTWBWAWCVTHU
      IZAUKZVSWDMPZRZHSZVSAMPZWBVTWFWDVIVLPZRZHSWHHVSVIVLMGULZVRUMWKWGHWKWJWFRW
      GWFWJUNWJWEWFWJVIWDKPWEWDVIKHULZVRUOABWDDEWMUPTUQTURTWFWIHADWDAVSMUSUTVSA
      DVAVBWAWFWDVIVNPZRZHSZVSBMPZWCHVSVIVNMWLVRUMWPWDBUKZWFRZHSWQWOWSHWOWNWFRW
      SWFWNUNWNWRWFWNVIWDOPWRWDVIOWMVRUOABWDDEWMVCTUQTURWFWQHBEWDBVSMUSUTTVSBEV
      AVBVGVSVIVMVOVDVSABVHVEVF $.
      $( [19-Apr-2014] $) $( [14-Apr-2014] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x $.
    brcap.1 $e |- A e. _V $.
    brcap.2 $e |- B e. _V $.
    brcap.3 $e |- C e. _V $.
    $( Binary relationship form of the ` Cap ` function.  (Contributed by Scott
       Fenton, 17-Apr-2014.) $)
    brcap $p |- ( <. A , B >. Cap C <-> C = ( A i^i B ) ) $=
      ( vx vy cvv cxp c1st ccnv cep ccom c2nd wbr wcel wa wex bitri ccap df-cap
      cop cin opex opelvv brxp mpbir2an cv wceq brco ancom brcnv br1steq anbi1i
      vex exbii breq2 ceqsexv epelcNEW 3bitri br2ndeq anbi12i 3bitr4ri brtxpsd3
      brin elin ) GABUCZCIIJZIJZUAKLZMNZOLZMNZUDZABUDZABUEZFUBVHCVJPVHVIQCIQABD
      UFFVHCVIIFUGUHGUIZVHVLPZVRVHVNPZRVRAQZVRBQZRVRVHVOPVRVPQVSWAVTWBVSHUIZAUJ
      ZVRWCMPZRZHSZVRAMPZWAVSWEWCVHVKPZRZHSWGHVRVHVKMGUPZVQUKWJWFHWJWIWERWFWEWI
      ULWIWDWEWIVHWCKPWDWCVHKHUPZVQUMABWCDEWLUNTUOTUQTWEWHHADWCAVRMURUSVRADUTVA
      VTWCBUJZWERZHSZVRBMPZWBVTWEWCVHVMPZRZHSWOHVRVHVMMWKVQUKWRWNHWRWQWERWNWEWQ
      ULWQWMWEWQVHWCOPWMWCVHOWLVQUMABWCDEWLVBTUOTUQTWEWPHBEWCBVRMURUSVRBEUTVAVC
      VRVHVLVNVFVRABVGVDVE $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A a b x $.  $d B a b x $.
    brsuccf.1 $e |- A e. _V $.
    brsuccf.2 $e |- B e. _V $.
    $( Binary relationship form of the ` Succ ` function.  (Contributed by
       Scott Fenton, 14-Apr-2014.) $)
    brsuccf $p |- ( A Succ B <-> B = suc A ) $=
      ( vx va vb ccup cid csingle wbr cun wceq csuccf cv wa wex vex bitri exbii
      ctxp ccom csn csuc cop brco w3a brtxp2 3anass ideq eqcom brsingle anbi12i
      anbi2i 2exbii anbi1i 19.41vv anass 3bitr2i excom opex breq1 syl6bb anbi2d
      brcup ceqsexv df-3an bitr4i snexALT uneq1 eqeq2d ceqsex2v 3bitri df-succf
      uneq2 breqi df-suc eqeq2i 3bitr4i ) ABHIJUAZUBZKZBAAUCZLZMZABNKBAUDZMWBAE
      OZVTKZWGBHKZPZEQWGFOZGOZUEZMZWKAMZWLWCMZPZWIPZPZGQZFQZEQZWEEABHVTCDUFWJXA
      EWJWNWQPZGQFQZWIPXCWIPZGQFQXAWHXDWIWHWNAWKIKZAWLJKZUGZGQFQXDFGAWGIJCERUHX
      HXCFGXHWNXFXGPZPXCWNXFXGUIXIWQWNXFWOXGWPXFAWKMWOAWKFRZUJAWKUKSAWLCGRZULUM
      UNSUOSUPXCWIFGUQXEWSFGWNWQWIURUOUSTXBWTEQZFQWOWPBWKWLLZMZUGZGQZFQWEWTEFUT
      XLXPFXLWSEQZGQXPWSEGUTXQXOGXQWQXNPZXOWRXREWMWKWLVAWNWIXNWQWNWIWMBHKXNWGWM
      BHVBWKWLBXJXKDVEVCVDVFWOWPXNVGVHTSTXNBAWLLZMWEFGAWCCAVIWOXMXSBWKAWLVJVKWP
      XSWDBWLWCAVOVKVLVMVMABNWAVNVPWFWDBAVQVRVS $.
      $( [19-Apr-2014] $) $( [14-Apr-2014] $)
  $}

  ${
    $d F x y z w $.
    $( The functional part of ` F ` is a function.  (Contributed by Scott
       Fenton, 16-Apr-2014.) $)
    funpartfun $p |- Fun Funpart F $=
      ( vx vy vz vw csingle cvv csingles cv wbr weq wal wcel vex bitri wceq wex
      wa anbi12i exbii cfunpart wfun cimage ccom cxp cin cdm cres wrel wi brres
      relres simplbi ancom csn cima eldm brsingle brimage snexALT imaeq2 eqeq2d
      brin brco ceqsexv 3bitri mpbiran elsingles 19.41v 3bitr4i excom eqeq1 cop
      elimasn df-br bitr4i eleq2 anbi12d equtr2 syl2anb syl6bir syl5bir exlimiv
      brxp elsn imp sylanb sylan2 ax-gen gen2 dffun2 mpbir2an df-funpart funeqi
      anassrs mpbir ) AUAZUBAAUCZFUDZGHUEZUFZUGZUHZUBZXDXCUIBIZCIZXCJZXEDIZXCJZ
      RCDKZUJZDLZCLBLAXBULXLBCXKDXIXGXEXHAJZXJXIXMXEXBMZXEXHAXBDNZUKUMXGXNXEXFA
      JZRZXMXJXGXPXNRXQXEXFAXBCNZUKXPXNUNOXNXPXMXJXNEIZUOZAXEUOZUPZPZEQZXPXMRZX
      JXNXEXFXAJZCQXFXTPZXFYBPZRZEQZCQZYDCXEXABNZUQYFYJCYFXEXFWSJZXEXFWTJZRZYJX
      EXFWSWTVCYHYGEQZRYPYHRYOYJYHYPUNYMYHYNYPYMXEXHFJZXHXFWRJZRZDQXHYAPZXFAXHU
      PZPZRZDQYHDXEXFWRFYLXRVDYSUUCDYQYTYRUUBXEXHYLXOURXHXFAXOXRUSSTUUBYHDYAXEU
      TYTUUAYBXFXHYAAVAVBVEVFYNXFHMZYPYNXEGMUUDYLXEXFGHXRWDVGEXFVHOSYGYHEVIVJOT
      YKYICQZEQYDYICEVKUUEYCEYHYCCXTXSUTXFXTYBVLVETOVFYDYEXJYCYEXJUJEYEXFYBMZXH
      YBMZRZYCXJUUFXPUUGXMUUFXEXFVMAMXPAXEXFYLXRVNXEXFAVOVPUUGXEXHVMAMXMAXEXHYL
      XOVNXEXHAVOVPSYCUUHXFXTMZXHXTMZRXJYCUUIUUFUUJUUGXTYBXFVQXTYBXHVQVRUUICEKD
      EKXJUUJCXSWEDXSWECDEVSVTWAWBWCWFWGWOWGWHWIWJBCDXCWKWLWQXCAWMWNWP $.
      $( [19-Apr-2014] $) $( [16-Apr-2014] $)
  $}

  $( The functional part of ` F ` is a subset of ` F ` .  (Contributed by Scott
     Fenton, 17-Apr-2014.) $)
  funpartss $p |- Funpart F C_ F $=
    ( cfunpart cimage csingle ccom cvv csingles cxp cin cres df-funpart eqsstri
    cdm resss ) ABAACDEFGHIMZJAAKAONL $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  ${
    $d A x $.  $d F x y $.
    $( The function value of the functional part is identical to the original
       functional value.  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    funpartfv $p |- ( Funpart F ` A ) = ( F ` A ) $=
      ( vx vy cfv csingle cvv csingles wcel wceq wn c0 csn cima cuni wex sylibr
      eleq1 wbr df-br cfunpart cimage ccom cxp cin cres df-funpart fveq1i fvres
      cdm nfvres dffv4 cv wral elsn wne noel vex eleq2 mpbiri mto nex elsingles
      snid mtbir mtbiri necon2ai imaeq2 ima0 syl6eq syl snprc bicomi necon2abii
      necon3i wi sneq imaeq2d eleq1d imbi12d wa eqid snex brimageg mpan snexALT
      wb breq1 ceqsexv brsingle bitr3i anbi12i exbii sylib opelco2g mpbird jctl
      cop opelxpg brin sylanbrc breq2 cla4egv mpd vtoclg mpcom con3i syl5ibrcom
      eldm notbid syl5bi ralrimiv disj unieqd uni0 unieqi syl5eq eqtr4d pm2.61i
      eqtri ) ABUAZEABBUBZFUCZGHUDZUEZUJZUFZEZABEZAYAYGBUGUHAYFIZYHYIJAYFBUIYJK
      ZYHLYIAYFBUKYKYIBAMZNZMZHUEZOZOZLABULYKYQLOZOZLYKYPYRYKYOLYKCUMZHIZKZCYNU
      NYOLJYKUUBCYNYTYNIYTYMJZYKUUBCYMUOYKUUBUUCYMHIZKUUDYJAGIZUUDYJUUDYLLUPZUU
      EUUDYMLUPUUFUUDYMLYMLJUUDLHIZUUGLYTMZJZCPUUICUUIYTLIZYTUQUUIUUJYTUUHIYTCU
      RZVDLUUHYTUSUTVAVBCLVCVEYMLHRVFVGYLLYMLYLLJZYMBLNLYLLBVHBVIVJVOVKUUEYLLUU
      EKUULAVLVMVNQBUUHNZHIZYTYFIZVPUUDYJVPCAGYTAJZUUNUUDUUOYJUUPUUMYMHUUPUUHYL
      BYTAVQVRVSYTAYFRVTUUNYTDUMZYESZDPZUUOUUNYTUUMYESZUUSUUNYTUUMYCSZYTUUMYDSZ
      UUTUUNYTUUMWRZYCIZUVAUUNUVDYTUUQWRFIZUUQUUMWRYBIZWAZDPZUUNUUQUUHJZUUQUUMY
      BSZWAZDPZUVHUUNUUHUUMYBSZUVLUUNUVMUUMUUMJZUUMWBUUHGIUUNUVMUVNWGYTWCUUHUUM
      BGHWDWEUTUVJUVMDUUHYTWFUUQUUHUUMYBWHWIQUVKUVGDUVIUVEUVJUVFUVIYTUUQFSUVEYT
      UUQUUKDURWJYTUUQFTWKUUQUUMYBTWLWMWNYTGIZUUNUVDUVHWGUUKDYTUUMYBFGHWOWEWPYT
      UUMYCTQUUNUVCYDIZUVBUUNUVPUVOUUNWAUUNUVOUUKWQYTUUMGHHWSWPYTUUMYDTQYTUUMYC
      YDWTXAUURUUTDUUMHUUQUUMYTYEXBXCXDDYTYEUUKXIQXEXFXGUUCUUAUUDYTYMHRXJXHXKXL
      CYNHXMQXNXNYSYRLYRLXOXPXOXTVJXQXRXSXT $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $( The full functional part of ` F ` is a function over ` _V ` .
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    fullfunfnv $p |- FullFun F Fn _V $=
      ( cfullfn cvv wfn cfunpart cdm cdif c0 csn cxp cun wa cin wceq funpartfun
      wfun funfn mpbi wf 0ex fconst ax-mp pm3.2i disjdif fnun df-fullfun fneq1i
      ffn mp2an undifv eqcomi fneq2i bitri mpbir ) ABZCDZAEZCUQFZGZHIZJZKZURUSK
      ZDZUQURDZVAUSDZLURUSMHNVDVEVFUQPVEAOUQQRUSUTVASVFUSHTUAUSUTVAUHUBUCURCUDU
      RUSUQVAUEUIUPVBCDVDCUOVBAUFUGCVCVBVCCURUJUKULUMUN $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d F x $.  $d A x $.
    $( The function value of the full function of ` F ` agrees with ` F ` .
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    fullfunfv $p |- ( FullFun F ` A ) = ( F ` A ) $=
      ( vx cvv wcel cfullfn cfv wceq cv fveq2 c0 wfn wa 0ex mp3an12 mpan eqtr4d
      wn pm2.61i fvprc eqeq12d cfunpart cdm cdif csn cxp cun df-fullfun disjdif
      fveq1i cin wfun funpartfun funfn mpbi wf fconst ffn ax-mp fvun1 vex eldif
      mpbiran fvun2 fvconst2 eqtrd sylbir ndmfv funpartfv 3eqtri vtoclg ) ADEZA
      BFZGZABGZHZCIZVMGZVQBGZHVPCADVQAHVRVNVSVOVQAVMJVQABJUAVRVQBUBZDVTUCZUDZKU
      EZUFZUGZGZVQVTGZVSVQVMWEBUHUJVQWAEZWFWGHZWAWBUKKHZWHWIWADUIZVTWALZWDWBLZW
      JWHMWIVTULWLBUMVTUNUOZWBWCWDUPWMWBKNUQWBWCWDURUSZWAWBVTWDVQUTOPWHRZWFKWGW
      PVQWBEZWFKHWQVQDEWPCVAVQDWAVBVCWQWFVQWDGZKWJWQWFWRHZWKWLWMWJWQMWSWNWOWAWB
      VTWDVQVDOPWBKVQNVEVFVGVQVTVHQSVQBVIVJVKVLRVNKVOAVMTABTQS $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    brfullfun.1 $e |- A e. _V $.
    brfullfun.2 $e |- B e. _V $.
    $( A binary relationship form condition for the full function.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    brfullfun $p |- ( A FullFun F B <-> B = ( F ` A ) ) $=
      ( cfullfn cfv wceq wbr eqcom cvv wfn wcel wb fullfunfnv fnbrfvb fullfunfv
      mp2an eqeq2i 3bitr3i ) ACFZGZBHZBUBHABUAIZBACGZHUBBJUAKLAKMUCUDNCODKABUAE
      PRUBUEBACQST $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A a b x $.  $d B a b x $.  $d C x $.
    brrestrict.1 $e |- A e. _V $.
    brrestrict.2 $e |- B e. _V $.
    brrestrict.3 $e |- C e. _V $.
    $( The binary relationship form of the ` Restrict ` function.  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    brrestrict $p |- ( <. A , B >. Restrict C <-> C = ( A |` B ) ) $=
      ( vx va vb cop ccap c1st ccart crange wbr wceq wa wex w3a bitri c2nd ccom
      ctxp crn cxp cin crestrict cres cv opex vex brtxp2 3anrot br1steq br2ndeq
      brco anbi1i exbii breq1 ceqsexv brrange 3bitri biid 3anbi123i 2exbii rnex
      opeq1 eqeq2d opeq2 ceqsex2v brcart brcap df-restrict breqi dfres3 3bitr4i
      xpex eqeq2i ) ABJZCKLMUANLUBZUCZUBZUCZUBZOZCABAUDZUEZUFZPZVSCUGOCABUHZPWE
      GUIZAWGJZPZWKCKOZQZGRZWLCKOZWIWEVSWKWCOZWNQZGRWPGVSCKWCABUJZFUPWSWOGWRWMW
      NWRWKHUIZIUIZJZPZVSXALOZVSXBWBOZSZIRHRXAAPZXBWGPZXDSZIRHRWMHIVSWKLWBWTGUK
      ZULXGXJHIXGXEXFXDSXJXDXEXFUMXEXHXFXIXDXDABXADEHUKZUNXFVSWKWAOZWKXBMOZQZGR
      ZBWFJZXBMOZXIGVSXBMWAWTIUKZUPXPWKXQPZXNQZGRXRXOYAGXMXTXNXMXDVSXAUAOZVSXBV
      TOZSZIRHRXABPZXBWFPZXDSZIRHRXTHIVSWKUAVTWTXKULYDYGHIYDYBYCXDSYGXDYBYCUMYB
      YEYCYFXDXDABXADEXLUOYCVSWKLOZWKXBNOZQZGRZAXBNOZYFGVSXBNLWTXSUPYKWKAPZYIQZ
      GRYLYJYNGYHYMYIABWKDEXKUNUQURYIYLGADWKAXBNUSUTTAXBDXSVAVBXDVCZVDTVEXDWKBX
      BJZPXTHIBWFEADVFZYEXCYPWKXABXBVGVHYFYPXQWKXBWFBVIVHVJVBUQURXNXRGXQBWFUJWK
      XQXBMUSUTTBWFXBEYQXSVKVBYOVDTVEXDWKAXBJZPWMHIAWGDBWFEYQVQZXHXCYRWKXAAXBVG
      VHXIYRWLWKXBWGAVIVHVJVBUQURTWNWQGWLAWGUJWKWLCKUSUTAWGCDYSFVLVBVSCUGWDVMVN
      WJWHCABVOVRVP $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    $d A a b f x y z $.  $d F a b f x y z $.
    $( A quantifier-free definition of the recursive definition generator.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    dfrdg4 $p |- rec ( F , A ) = U.
       ( ( Funs i^i ( `' Domain " On ) ) \
         dom ( ( `' _E o. Domain ) \
         Fix ( `' Apply o.
               ( ( ( _V X. { (/) } ) X. { U. { A } } ) u.
                 ( ( ( Bigcup o. Img ) |` ( _V X. Limits ) ) u.
                   ( ( FullFun F o. ( Apply o. pprod ( _I , Bigcup ) ) ) |`
                     ( _V X. ran Succ ) ) ) ) ) ) ) $=
      ( vx vy vz va vb c0 wceq cvv wcel cuni wa con0 capply wex 3bitri bitri wn
      wbr vf crdg cv wfn cfv cif wlim cima wral wrex cab cfuns cdomain ccnv cin
      cep ccom csn cxp cbigcup cimg climits cres cfullfn cid cpprod csuccf cfix
      crn cun cdif cdm dfrdg3 wfun an12 df-fn ancom eqcom anbi1i anass vex dmex
      exbii eleq1 raleq anbi12d ceqsexv df-rex brcnv anbi12i brco breq1 w3a cop
      wi wb wo brun opelxp mpbiran elsn brres opex brbigcup ax-mp unieq brapply
      eqeq2d fvex orbi12i limeq mtbiri intnanrd mpbiri df-ne sylib orel2 iftrue
      wne syl biimprd adantld biimpd anc2li syl6 impbid rexlimivw orel1 iffalse
      orc fveq2d ifbieq2d 3syld olcd con2i adantl syl5bb syl6bb exbidv syl6bbr
      anbi2d eldif elin elfuns elima brdomain wal brdif epelcNEW onelon 3adant1
      csuc brxp brimg imaexg eqeq1d brpprod3a 3anrot ideq equcom biid 3anbi123i
      ellimits 2exbii uniex opeq1 opeq2 ceqsex2v brfullfun fveq2 elrn w3o onzsl
      brsuccf nlim0 nsuceq0 neeq2 necomd nexdv ioran unisnif syl6eqr syld neeq1
      sylanbrc nlimsucg mpbi eqtri eqeq1 3eqtr4a rexex olc ee12an exlimiv eqtrd
      mp2b 3jaoi sylbi a1i df-br elfix eqvinc 3bitr4g notbid 3expia annim exnal
      pm5.32d syl6rbb eldm con1bid df-ral pm5.32i 3bitr4ri abbi2i unieqi eqtr4i
      ) BAUBUAUCZCUCZUDZDUCZUXRUEZUYAHIZAJKAHUFZUYAUGZUXRUYAUHZLZUYALZUXRUEZBUE
      ZUFZUFZIZDUXSUIZMZCNUJZUAUKZLULUMUNZNUHZUOZUPUNZUMUQZOUNZJHURZUSZAURLZURZ
      USZUTVAUQZJVBUSZVCZBVDZOVEUTVFZUQZUQZJVGVIZUSZVCZVJZVJZUQZVHZVKZVLZVKZLCD
      UABAVMVVEUYQUYPUAVVEUXSNKZUYOMZCPZUXRVNZUXRVLZNKZUYMDVVJUIZMZMZUYPUXRVVEK
      ZVVHUXSVVJIZVVIVVFUYNMZMZMZCPVVNVVGVVSCVVGUXTVVQMVVPVVIMZVVQMVVSVVFUXTUYN
      VOUXTVVTVVQUXTVVIVVJUXSIZMVWAVVIMVVTUXRUXSVPVVIVWAVQVWAVVPVVIVVJUXSVRVSQV
      SVVPVVIVVQVTQWCVVRVVNCVVJUXRUAWAZWBZVVPVVQVVMVVIVVPVVFVVKUYNVVLUXSVVJNWDZ
      UYMDUXSVVJWEWFUUAWGRUYOCNWHVVOVVIVVKMZUXRVVDKZSZMZVWEVVLMVVNVVOUXRUYTKZVW
      GMVWHUXRUYTVVDUUBVWIVWEVWGVWIUXRULKZUXRUYSKZMVWEUXRULUYSUUCVWJVVIVWKVVKUX
      RVWBUUDVWKUXSUXRUYRTZCNUJVVFVWLMZCPZVVKCUXRUYRNVWBUUEVWLCNWHVWNVVPVVFMZCP
      VVKVWMVWOCVWMVWLVVFMVWOVVFVWLVQVWLVVPVVFVWLUXRUXSUMTZVVPUXSUXRUMCWAZVWBWI
      UXRUXSVWBVWQUUFZRVSRWCVVFVVKCVVJVWCVWDWGRQWJRVSRVWEVWGVVLVWEVWGUYAVVJKZUY
      MWOZDUUGZVVLVWEVXAVWFVWEVXASZUXRUYAVVCTZDPZVWFVWEVXDVWTSZDPVXBVWEVXCVXEDV
      XCVWSUXRUYAVVBTZSZMZVWEVXEVXCUXRUYAVUBTZVXGMVXHUXRUYAVUBVVBUUHVXIVWSVXGVX
      IVWPUXSUYAVUATZMZCPZVVJUYAVUATZVWSCUXRUYAVUAUMVWBDWAZWKVXLVVPVXJMZCPVXMVX
      KVXOCVWPVVPVXJVWRVSWCVXJVXMCVVJVWCUXSVVJUYAVUAWLWGRVXMUYAVVJUPTVWSVVJUYAU
      PVWCVXNWIUYAVVJVWCUUIRQVSRVWEVXHVWSUYMSZMVXEVWEVWSVXGVXPVVIVVKVWSVXGVXPWP
      VVIVVKVWSWMZVXFUYMVXQUXRUYAWNZUXSVUTTZUXSVXRVUCTZMZCPZUXSUYBIZUXSUYLIZMZC
      PVXFUYMVXQVYAVYECVXQVYAVYDVYCMVYEVXQVXSVYDVXTVYCVXQUYANKZVXSVYDWPVVKVWSVY
      FVVIVVJUYAUUJUUKVXSUYCUXSVUFIZMZUYEUXSUYGIZMZUYAEUCZUULZIZEPZUXSUYJIZMZWQ
      ZWQZVYFVYDVXSVXRUXSVUHTZVXRUXSVUSTZWQVYRVXRUXSVUHVUSWRVYSVYHVYTVYQVYSVXRV
      UEKZUXSVUGKZMVYHVXRUXSVUEVUGVWQUUMWUAUYCWUBVYGWUAUYAVUDKZUYCWUAUXRJKZWUCV
      WBUXRUYAJVUDVXNWSWTDHXARCVUFXAWJRVYTVXRUXSVUKTZVXRUXSVURTZWQVYQVXRUXSVUKV
      URWRWUEVYJWUFVYPWUEVXRUXSVUITZVXRVUJKZMVYIUYEMVYJVXRUXSVUIVUJVWQXBWUGVYIW
      UHUYEWUGVXRVYKVATZVYKUXSUTTZMZEPVYKUYFIZVYKLZUXSIZMZEPZVYIEVXRUXSUTVAUXRU
      YAXCZVWQWKWUKWUOEWUIWULWUJWUNUXRUYAVYKVWBVXNEWAZUUNVYKUXSVWQXDWJWCWUPUYGU
      XSIZVYIWUNWUSEUYFWUDUYFJKVWBUXRUYAJUUOXEWULWUMUYGUXSVYKUYFXFUUPWGUYGUXSVR
      RQWUHUYAVBKZUYEWUHWUDWUTVWBUXRUYAJVBVXNWSWTUYAVXNUVCRWJVYIUYEVQQWUFVXRUXS
      VUOTZVXRVUQKZMVYOVYNMVYPVXRUXSVUOVUQVWQXBWVAVYOWVBVYNWVAVXRFUCZVUNTZWVCUX
      SVULTZMZFPWVCUYIIZUXSWVCBUEZIZMZFPVYOFVXRUXSVULVUNWUQVWQWKWVFWVJFWVDWVGWV
      EWVIWVDVXRVYKVUMTZVYKWVCOTZMZEPVYKUXRUYHWNZIZWVLMZEPZWVGEVXRWVCOVUMWUQFWA
      ZWKWVMWVPEWVKWVOWVLWVKVYKWVCGUCZWNZIZUXRWVCVETZUYAWVSUTTZWMZGPFPWVCUXRIZW
      VSUYHIZWWAWMZGPFPWVOFGVEUTUXRUYAVYKVWBVXNWURUUQWWDWWGFGWWDWWBWWCWWAWMWWGW
      WAWWBWWCUURWWBWWEWWCWWFWWAWWAWWBUXRWVCIWWEUXRWVCWVRUUSUAFUUTRWWCUYHWVSIWW
      FUYAWVSGWAXDUYHWVSVRRWWAUVAUVBRUVDWWAVYKUXRWVSWNZIWVOFGUXRUYHVWBUYAVXNUVE
      ZWWEWVTWWHVYKWVCUXRWVSUVFXHWWFWWHWVNVYKWVSUYHUXRUVGXHUVHQVSWCWVQWVNWVCOTZ
      WVGWVLWWJEWVNUXRUYHXCVYKWVNWVCOWLWGUXRUYHWVCVWBWWIWVRXGRQWVCUXSBWVRVWQUVI
      WJWCWVIVYOFUYIUYHUXRXIWVGWVHUYJUXSWVCUYIBUVJXHWGQWVBUYAVUPKZVYKUYAVGTZEPV
      YNWVBWUDWWKVWBUXRUYAJVUPVXNWSWTEUYAVGVXNUVKWWLVYMEVYKUYAWURVXNUVNWCQWJVYO
      VYNVQQXJRXJRVYFUYCVYMENUJZUYAJKZUYEMZUVLVYRVYDWPZEUYAUVMUYCWWPWWMWWOUYCVY
      RVYDUYCVYRVYHVYDUYCVYQSZVYRVYHWOUYCVYJSZVYPSZWWQUYCUYEVYIUYCUYEHUGUVOUYAH
      XKXLZXMUYCVYNVYOUYCVYMEUYCUYAVYLXSVYMSUYCVYLUYAUYCVYLUYAXSVYLHXSZVYKUVPZU
      YAHVYLUVQXNUVRUYAVYLXOXPUVSXMVYJVYPUVTUWEVYQVYHXQXTUYCVYGVYDUYCUYCVYDVYGU
      YCUYLVUFUXSUYCUYLUYDVUFUYCUYDUYKXRAUWAUWBXHZYAYBUWCUYCVYDVYHVYRUYCVYDVYGU
      YCVYDVYGWXCYCYDVYHVYQYJYEYFWWMVYRVYDWWMVYRVYQVYPVYDWWMVYHSZVYRVYQWOZVYMWX
      DENVYMUYCVYGVYMUYAHXSZUYCSZVYMWXFWXAWXBUYAVYLHUWDXNUYAHXOXPXMYGVYHVYQYHZX
      TWWMWWRVYQVYPWOWWMUYEVYIVYMUYESZENVYMUYEVYLUGZVYKJKZWXJSZWURVYKJUWFZXEUYA
      VYLXKZXLZYGXMVYJVYPYHXTWWMVYOVYDVYNWWMVYDVYOWWMUYLUYJUXSVYMUYLUYJIENVYMVY
      LHIZUYDWXJUYGVYLLZUXRUEZBUEZUFZUFZWXSUYLUYJWYAWXTWXSWXPSZWYAWXTIWXAWYBWXB
      VYLHXOUWGWXPUYDWXTYIXEWXKWXLWXTWXSIWURWXMWXJUYGWXSYIUWPUWHVYMUYCWXPUYKWXT
      UYDUYAVYLHUWIVYMUYEWXJUYJWXSUYGWXNVYMUYIWXRBVYMUYHWXQUXRUYAVYLXFYKYKZYLYL
      WYCUWJYGXHZYAYBYMWWMVYNVYDVYOVYRVYMENUWKWWMVYDVYOWYDYCVYPVYQVYHVYPVYJUWLY
      NUWMYFWWOVYRVYDUYEVYRVYDWOWWNUYEVYRVYQVYJVYDUYEWXDWXEUYEUYCVYGUYCUYEWWTYO
      ZXMWXHXTUYEWWSVYQVYJWOUYEVYNVYOVYNUYEVYMWXIEWXOUWNYOXMVYPVYJXQXTUYEVYIVYD
      UYEUYEVYDVYIUYEUYLUYGUXSUYEUYLUYKUYGUYEWXGUYLUYKIWYEUYCUYDUYKYIXTUYEUYGUY
      JXRUWOXHZYAYBYMYPUYEVYDVYRWOWWNUYEVYDVYJVYRUYEVYDVYIUYEVYDVYIWYFYCYDVYJVY
      QVYHVYJVYPYJYNYEYPYFUWQUWRYQXTVXTVYCWPVXQVXTVXRUXSOTVYCUXSVXROVWQWUQWIUXR
      UYAUXSVWBVXNVWQXGRUWSWFVYDVYCVQYRYSVXFVXRVVBKVXRVXRVVATVYBUXRUYAVVBUWTVXR
      VVAWUQUXACVXRVXRVUCVUTWUQWUQWKQCUYBUYLUYAUXRXIUXBUXCUXDUXEUXHVWSUYMUXFYRY
      QYSVWTDUXGUXIDUXRVVCVWBUXJYTUXKUYMDVVJUXLYTUXMVVIVVKVVLVTQUXNUXOUXPUXQ $.
      $( [19-Apr-2014] $) $( [11-Apr-2014] $)
  $}

  ${
    $d f x $.  $d f y $.  $d f z $.  $d G f $.  $d G x $.  $d G y $.  $d G z $.
    $d x y $.  $d x z $.  $d y z $.

    $( Calculate a quantifier-free version of the function from ~ tfr1 through
       ~ tfr3 .  (Contributed by Scott Fenton, 29-Apr-2014.) $)
    tfrqfree $p |- ( ( Funs i^i ( `' Domain " On ) ) \
         dom ( ( `' _E o. Domain ) \
         Fix ( `' Apply o. ( FullFun G o. Restrict ) ) ) ) =
         { f | E. x e. On ( f Fn x /\ A. y e. x ( f ` y ) =
               ( G ` ( f |` y ) ) ) } $=
      ( vz cv wceq wa con0 wrex cdomain wcel wn vex wbr bitri anbi12i wex exbii
      3bitri wfn cfv cres wral cfuns ccnv cima cin cep capply cfullfn crestrict
      ccom cfix cdif cdm wfun elfuns brcnv brdomain rexbii elima risset 3bitr4i
      elin cop eldm2 eldif brco df-br wel epel dmex eleq2 ceqsexv 3bitr3i elfix
      ancom brapply brrestrict brfullfun resex fveq2 eqeq2d eqeq1 notbii df-rex
      opex rexnal 3bitr2ri con1bii anass df-fn eqcom anbi1i eleq1 raleq anbi12d
      fvex an12 anbi2d abbi2i ) CFZAFZUAZBFZXCUBZXCXFUCZDUBZGZBXDUDZHZAIJZCUEKU
      FZIUGZUHZUIUFZKUMZUJUFZDUKZULUMZUMZUNZUOZUPZUOZXCXPLZXCYELZMZHZXCUQZXCUPZ
      ILZXJBYLUDZHZHZXCYFLXMYJYKYMHZYNHYPYGYQYIYNYGXCUELZXCXOLZHYQXCUEXOVEYRYKY
      SYMXCCNZURXDXCXNOZAIJXDYLGZAIJYSYMUUAUUBAIUUAXCXDKOZUUBXDXCKANZYTUSXCXDYT
      UUDUTZPVAAXCXNIYTVBAYLIVCVDQPYNYHYHXFYLLZXJMZHZBRZUUGBYLJYNMYHXCXFVFZYDLZ
      BRUUIBXCYDYTVGUUKUUHBUUKUUJXRLZUUJYCLZMZHUUHUUJXRYCVHUULUUFUUNUUGXCXFXROU
      UCXDXFXQOZHZARZUULUUFAXCXFXQKYTBNZVIXCXFXRVJUUQUUBBAVKZHZARUUFUUPUUTAUUCU
      UBUUOUUSUUEUUOXFXDUIOUUSXDXFUIUUDUURUSBAVLPQSUUSUUFAYLXCYTVMZXDYLXFVNVOPV
      PUUMXJUUMUUJUUJYBOUUJXDYAOZXDUUJXSOZHZARZXJUUJYBXCXFWHZVQAUUJUUJXSYAUVFUV
      FVIUVEXDXGGZXDXIGZHZARXJUVDUVIAUVDUVCUVBHUVIUVBUVCVRUVCUVGUVBUVHUVCUUJXDU
      JOUVGXDUUJUJUUDUVFUSXCXFXDYTUURUUDVSPUVBUUJEFZULOZUVJXDXTOZHZERUVJXHGZXDU
      VJDUBZGZHZERUVHEUUJXDXTULUVFUUDVIUVMUVQEUVKUVNUVLUVPXCXFUVJYTUURENZVTUVJX
      DDUVRUUDWAQSUVPUVHEXHXCXFYTWBUVNUVOXIXDUVJXHDWCWDVOTQPSUVHXJAXGXFXCWSXDXG
      XIWEVOPTWFQPSPUUGBYLWGXJBYLWIWJWKQYKYMYNWLPXCXPYEVHXMXDILZXLHZARUUBYKUVSX
      KHZHZHZARYPXLAIWGUVTUWCAUVTXEUWAHUUBYKHZUWAHUWCUVSXEXKWTXEUWDUWAXEYKYLXDG
      ZHUWEYKHUWDXCXDWMYKUWEVRUWEUUBYKYLXDWNWOTWOUUBYKUWAWLTSUWBYPAYLUVAUUBUWAY
      OYKUUBUVSYMXKYNXDYLIWPXJBXDYLWQWRXAVOTVDXB $.
      $( [29-Apr-2014] $)
  $}

