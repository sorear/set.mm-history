$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( Any set that contains one element less than the universe is not equal to
       it.  (Contributed by Andrew Salmon, 16-Jun-2011.) $)
    elnev $p |- ( A e. _V <-> { x | -. x = A } =/= _V ) $=
      ( cv wceq wex wn cab cvv wcel wne wal df-v eqeq2i wb equid tbt albii abbi
      bitr2i alnex 3bitri con2bii isset df-ne 3bitr4i ) ACZBDZAEZUGFZAGZHDZFBHI
      UJHJUKUHUKUJUFUFDZAGZDZUIAKZUHFHUMUJALMUOUIULNZAKUNUIUPAULUIAOPQUIULARSUG
      ATUAUBABUCUJHUDUE $.
      $( [16-Jun-2011] $)
  $}

  ${
    $( A version of Russell's paradox which is proven using proper
       substitution.  (Contributed by Andrew Salmon, 18-Jun-2011.) $)
    rusbcALT $p |- { x | x e/ x } e/ _V $=
      ( cv wnel cab cvv wcel wn pm5.19 wsbc csb elabsg sbcnel12g df-nel csbvarg
      wb eleq12d notbid syl5bb 3bitrd mto mpbir ) ABZUBCZADZECUDEFZGUEUDUDFZUFG
      ZOUFHUEUFUCAUDIAUDUBJZUHCZUGUCAUDEKAUDUBUBELUIUHUHFZGUEUGUHUHMUEUJUFUEUHU
      DUHUDAUDENZUKPQRSTUDEMUA $.
      $( [18-Jun-2011] $)
  $}

  ${
    $d x A $.
    $( Equivalence between two ways of saying "is a member of the complement of
       ` A ` ."  (Contributed by Andrew Salmon, 15-Jul-2011.) $)
    compel $p |- ( x e. ( _V \ A ) <-> -. x e. A ) $=
      ( cv cvv cdif wcel wn vex eldif mpbiran ) ACZDBEFKDFKBFGAHKDBIJ $.
      $( [15-Jul-2011] $)

    $( Equality between two ways of saying "the complement of ` A ` ."
       (Contributed by Andrew Salmon, 15-Jul-2011.) $)
    compeq $p |- ( _V \ A ) = { x | -. x e. A } $=
      ( cv wcel wn cvv cdif compel abbi2i ) ACBDEAFBGABHI $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d A x y $.
    $( The complement of ` A ` is not equal to ` A ` .  (Contributed by Andrew
       Salmon, 15-Jul-2011.) $)
    compne $p |- ( _V \ A ) =/= A $=
      ( vx vy cvv cdif wne wceq wn cv wcel cab wb wal wsbc ax-4 df-clab bibi12i
      pm5.19 sbn bibi1i bicom 3bitri sylib mto dfcleq mtbir compeq eqcomi abid2
      eqeq12i mtbi df-ne mpbir ) DAEZAFUNAGZHBIAJZHZBKZUPBKZGZUOUTCIZURJZVAUSJZ
      LZCMZVEUPBVANZVFHZLZVFRVEVDVHVDCOVDUQBVANZVFLVGVFLVHVBVIVCVFUQCBPUPCBPQVI
      VGVFUPBCSTVGVFUAUBUCUDCURUSUEUFURUNUSAUNURBAUGUHBAUIUJUKUNAULUM $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d x ph $.  $d x z $.
    $( Two ways of saying "the complement of a class abstraction".
       (Contributed by Andrew Salmon, 15-Jul-2011.) $)
    compab $p |- ( _V \ { z | ph } ) = { z | -. ph } $=
      ( vx cvv cab cdif cv wcel wn compeq hbab1 hbn ax-17 wceq df-clab sbequ12r
      wsbc syl5bb notbid cbvab eqtri ) DABEZFCGZUBHZIZCEAIZBECUBJUEUFCBUDBABCKL
      UFCMUCBGNZUDAUDABUCQUGAACBOACBPRSTUA $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Contrpositive law for subsets.  (Contributed by Andrew Salmon,
       15-Jul-2011.) $)
    conss34 $p |- ( A C_ B <-> ( _V \ B ) C_ ( _V \ A ) ) $=
      ( vx cv wcel wi wal cvv cdif wss con34b compel imbi12i bitr4i albii dfss2
      wn 3bitr4i ) CDZAEZSBEZFZCGSHBIZEZSHAIZEZFZCGABJUCUEJUBUGCUBUAQZTQZFUGTUA
      KUDUHUFUICBLCALMNOCABPCUCUEPR $.
      $( [15-Jul-2011] $)

    $( Contrapositive law for subsets.  (Contributed by Andrew Salmon,
       15-Jul-2011.) $)
    conss2 $p |- ( A C_ ( _V \ B ) <-> B C_ ( _V \ A ) ) $=
      ( cvv wss cdif wb ssv ssconb mp2an ) ACDBCDACBEDBCAEDFAGBGABCHI $.
      $( [15-Jul-2011] $)

    $( Contrapositive law for subsets.  (Contributed by Andrew Salmon,
       15-Jul-2011.) $)
    conss1 $p |- ( ( _V \ A ) C_ B <-> ( _V \ B ) C_ A ) $=
      ( cvv difcom ) CABD $.
      $( [15-Jul-2011] $)
  $}

  ${
    ralbidar.1 $e |- ( ph -> A. x e. A ph ) $.
    ralbidar.2 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( More general form of ~ ralbida .  (Contributed by Andrew Salmon,
       25-Jul-2011.) $)
    ralbidar $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral wb ex ralimi syl df-ral sylib pm2.43 pm5.74d alimi
      albi 3syl 3bitr4g ) ADHEIZBJZDKZUECJZDKZBDELCDELAUEUEBCMZJZJZDKZUFUHMZDKU
      GUIMAUKDELZUMAADELUOFAUKDEAUEUJGNOPUKDEQRULUNDULUEBCUEUJSTUAUFUHDUBUCBDEQ
      CDEQUD $.
      $( [25-Jul-2011] $)

    $( More general form of ~ rexbida .  (Contributed by Andrew Salmon,
       25-Jul-2011.) $)
    rexbidar $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( cv wcel wa wex wrex wb wi wal wral ex ralimi syl df-rex sylib exbi 3syl
      df-ral pm2.43 pm5.32d alimi 3bitr4g ) ADHEIZBJZDKZUICJZDKZBDELCDELAUIUIBC
      MZNZNZDOZUJULMZDOUKUMMAUODEPZUQAADEPUSFAUODEAUIUNGQRSUODEUDUAUPURDUPUIBCU
      IUNUEUFUGUJULDUBUCBDETCDETUH $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d ph w $.  $d w x $.  $d w y $.  $d w z $.
    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction.  (Contributed by Andrew Salmon, 25-Jul-2011.) $)
    dropab1 $p |- ( A. x x = y -> { <. x , z >. | ph } =
        { <. y , z >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq1 a4s eqeq2d anbi1d drex2 drex1
      df-opab abbidv 3eqtr4g ) BFZCFZGZBHZEFZUCDFZIZGZAJZDKZBKZELUGUDUHIZGZAJZD
      KZCKZELABDMACDMUFUMUREULUQBCUKUPBCDUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOPQRSUAA
      BDETACDETUB $.
      $( [25-Jul-2011] $)

    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction.  (Contributed by Andrew Salmon, 25-Jul-2011.) $)
    dropab2 $p |- ( A. x x = y -> { <. z , x >. | ph } =
        { <. z , y >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq2 a4s eqeq2d anbi1d drex1 drex2
      df-opab abbidv 3eqtr4g ) BFZCFZGZBHZEFZDFZUCIZGZAJZBKZDKZELUGUHUDIZGZAJZC
      KZDKZELADBMADCMUFUMUREULUQBCDUKUPBCUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOPQRSUAA
      DBETADCETUB $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( If the identity relation partially orders any class, then that class is
       the null class.  (Contributed by Andrew Salmon, 25-Jul-2011.) $)
    ipo0 $p |- ( _I Po A <-> A = (/) ) $=
      ( vx cid wpo c0 wceq cv wcel wbr weq equid vex ideq mpbir wn poirr eq0rdv
      ex mt2i po0 poeq2 mpbiri impbii ) ACDZAEFZUDBAUDBGZAHZUFUFCIZUHBBJBKUFUFB
      LMNUDUGUHOAUFCPRSQUEUDECDCTAECUAUBUC $.
      $( [25-Jul-2011] $)

    $( A class that is founded by the identity relation is null.  (Contributed
       by Andrew Salmon, 25-Jul-2011.) $)
    ifr0 $p |- ( _I Fr A <-> A = (/) ) $=
      ( vx cid wfr c0 wceq cv wcel wbr equid ideq mpbir wn frirr ex mt2i eq0rdv
      vex fr0 freq2 mpbiri impbii ) ACDZAEFZUCBAUCBGZAHZUEUECIZUGUEUEFBJUEUEBRK
      LUCUFUGMBACNOPQUDUCECDCSAECTUAUB $.
      $( [25-Jul-2011] $)

    $( ~ ordelpss with an antecedent removed.  (Contributed by Andrew Salmon,
       25-Jul-2011.) $)
    ordpss $p |- ( Ord B -> ( A e. B -> A C. B ) ) $=
      ( word wcel wa wpss ordelord ex ancrd ordelpss ancoms biimpd expimpd syld
      wb ) BCZABDZACZQEABFZPQRPQRBAGHIPRQSPREQSRPQSOABJKLMN $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    fvsb.1 $e |- A e. _V $.
    $( Explicit substitution of a value of a function into a wff.  (Contributed
       by Andrew Salmon, 1-Aug-2011.) $)
    fvsb $p |- ( E! y A F y -> ( [ ( F ` A ) / x ] ph <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cfv wsbc cv wbr cio weu wceq wb wal wa wex fv4 dfsbcq ax-mp iotasbc
      syl5bb ) ABDEGZHZABDCIZEJZCKZHZUFCLUFUEBIMNCOAPBQUCUGMUDUHNCDEFRABUCUGSTU
      FACBUAUB $.
      $( [1-Aug-2011] $)

    fveqsb.2 $e |- ( x = ( F ` A ) -> ( ph <-> ps ) ) $.
    fveqsb.3 $e |- ( ps -> A. x ps ) $.
    $( Implicit substitution of a value of a function into a wff.  (Contributed
       by Andrew Salmon, 1-Aug-2011.) $)
    fveqsb $p |- ( E! y A F y -> ( ps <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cfv wsbc cv wbr weu wceq wb wal wa wex cvv wcel fvex sbciegf ax-mp fvsb
      wi a1i syl5bbr ) BACEFJZKZEDLZFMZDNULUKCLOPDQARCSUITUAZUJBPEFUBABCUITBBCQ
      UFUMIUGHUCUDACDEFGUEUH $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d x y z A $.  $d x y B $.
    $( 'Less than' implies not equal for extended reals.  (Contributed by
       Andrew Salmon, 11-Nov-2011.) $)
    xrltneNEW $p |- ( ( A e. RR* /\ A < B ) -> A =/= B ) $=
      ( cxr wcel clt wbr wne wn wceq xrltnr breq2 notbid syl5ibcom necon2ad imp
      ) ACDZABEFZABGPQABPAAEFZHABIZQHAJSRQABAEKLMNO $.
      $( [11-Nov-2011] $)

    $( A Cartesian product exists iff its converse does.  Corollary 6.9(1) in
       [TakeutiZaring] p. 26.  (Contributed by Andrew Salmon, 13-Nov-2011.) $)
    xpexb $p |- ( ( A X. B ) e. _V <-> ( B X. A ) e. _V ) $=
      ( cxp cvv wcel ccnv cnvxp cnvexg syl5eqelr impbii ) ABCZDEZBACZDEZLMKFDAB
      GKDHINKMFDBAGMDHIJ $.
      $( [13-Nov-2011] $)

    $( A condition where the converse of ~ xpex holds as well.  Corollary
       6.9(2) in [TakeutiZaring] p. 26.  (Contributed by Andrew Salmon,
       13-Nov-2011.) $)
    xpexcnv $p |- ( ( B =/= (/) /\ ( A X. B ) e. _V ) -> A e. _V ) $=
      ( c0 wne cxp cvv wcel cdm dmexg dmxp eleq1d syl5ib imp ) BCDZABEZFGZAFGZP
      OHZFGNQOFINRAFABJKLM $.
      $( [13-Nov-2011] $)

    $( An element of a transitive set is a proper subset of it.  Theorem 7.2 in
       [TakeutiZaring] p. 35.  Unlike ~ tz7.2 , ~ ax-reg is required for its
       proof.  (Contributed by Andrew Salmon, 13-Nov-2011.) $)
    trelpss $p |- ( ( Tr A /\ B e. A ) -> B C. A ) $=
      ( wtr wcel wa wss wne wpss cep wfr zfregfr tz7.2 mp3an2 df-pss sylibr ) A
      CZBADZEBAFBAGEZBAHPAIJQRAKABLMBANO $.
      $( [13-Nov-2011] $)
  $}


