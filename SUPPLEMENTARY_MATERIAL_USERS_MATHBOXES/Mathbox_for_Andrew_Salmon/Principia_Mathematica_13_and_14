$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Principia Mathematica * 13 and * 14
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( One result of theorem *13.13 in [WhiteheadRussell] p. 178.  A note on the
     section - to make the theorems more usable, and because inequality is
     notation for set theory (it is not defined in the predicate calculus
     section), this section will use classes instead of sets.  (Contributed by
     Andrew Salmon, 3-Jun-2011.) $)
  pm13.13a $p |- ( ( ph /\ x = A ) -> [ A / x ] ph ) $=
    ( cv wceq wsbc sbceq1a biimpac ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $)

  $( Theorem *13.13 in [WhiteheadRussell] p. 178 with different variable
     substitution.  (Contributed by Andrew Salmon, 3-Jun-2011.) $)
  pm13.13b $p |- ( ( [ A / x ] ph /\ x = A ) -> ph ) $=
    ( cv wceq wsbc sbceq1a biimparc ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $)

  $( Theorem *13.14 in [WhiteheadRussell] p. 178.  (Contributed by Andrew
     Salmon, 3-Jun-2011.) $)
  pm13.14 $p |- ( ( [ A / x ] ph /\ -. ph ) -> x =/= A ) $=
    ( wsbc wn cv wne wceq sbceq1a biimprcd necon3bd imp ) ABCDZAEBFZCGMANCNCHAM
    ABCIJKL $.
    $( [3-Jun-2011] $)

  ${
    $d y A z $.  $d y B z $.
    $( Compare theorem *13.183 in [WhiteheadRussell] p. 178.  Only ` A ` is
       required to be a set.  (Contributed by Andrew Salmon, 3-Jun-2011.) $)
    pm13.183 $p |- ( A e. V -> ( A = B <-> A. z ( z = A <-> z = B ) ) ) $=
      ( vy cv wceq wal eqeq1 eqeq2 bibi1d albidv alrimiv wsbc stdpc4 sbbi eqsb3
      wb bibi2i sylbi equsb1 bi1 mpi syl impbii vtoclbg ) EFZCGZAFZUGGZUICGZRZA
      HZBCGUIBGZUKRZAHEBDUGBCIUGBGZULUOAUPUJUNUKUGBUIJKLUHUMUHULAUGCUIJMUMULAUG
      NZUHULAEOUQUJAUGNZUKAUGNZRZUHUJUKAEPUTURUHRZUHUSUHUREACQSVAURUHAEUAURUHUB
      UCTTUDUEUF $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d ph z $.
    $( Theorem *13.192 in [WhiteheadRussell] p. 179.  (Contributed by Andrew
       Salmon, 3-Jun-2011.) $)
    pm13.192 $p |- ( A e. _V -> ( E. y ( A. x ( x = A <-> x = y ) /\ ph ) <->
      [ A / y ] ph ) ) $=
      ( cvv wcel wsbc cv wceq wa wex wb wal sbc5g bi2 alimi ax-17 eqeq1 equsal
      wi sylib eqeq2 eqcoms alrimiv impbii anbi1i exbii syl6rbbr ) DEFACDGCHZDI
      ZAJZCKBHZDIZULUIIZLZBMZAJZCKACDENUQUKCUPUJAUPUJUPUNUMTZBMUJUOURBUMUNOPUMU
      JBCUJBQULUIDRSUAUJUOBUODUIDUIULUBUCUDUEUFUGUH $.
      $( [3-Jun-2011] $)
  $}

  $( Theorem *13.193 in [WhiteheadRussell] p. 179.  (Contributed by Andrew
     Salmon, 3-Jun-2011.) $)
  pm13.193 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ x = y ) ) $=
    ( weq wsb sbequ12 pm5.32ri ) BCDAABCEABCFG $.
    $( [3-Jun-2011] $)

  $( Theorem *13.194 in [WhiteheadRussell] p. 179.  (Contributed by Andrew
     Salmon, 3-Jun-2011.) $)
  pm13.194 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ ph /\ x = y ) ) $=
    ( cv wceq wa wsbc w3a pm13.13a simpl simpr 3jca 3simpc impbii ) ABDCDZEZFZA
    BOGZAPHQRAPABOIAPJAPKLRAPMN $.
    $( [3-Jun-2011] $)

  ${
    $d y A $.
    pm13.195.1 $e |- A e. _V $.
    $( Theorem *13.195 in [WhiteheadRussell] p. 179.  This theorem is very
       similar to sbc5.  (Contributed by Andrew Salmon, 3-Jun-2011.) $)
    pm13.195 $p |- ( E. y ( y = A /\ ph ) <-> [ A / y ] ph ) $=
      ( wsbc cv wceq wa wex sbc5 bicomi ) ABCEBFCGAHBIABCDJK $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *13.196 in [WhiteheadRussell] p. 179.  The only difference is
       the position of the substituted variable.  (Contributed by Andrew
       Salmon, 3-Jun-2011.) $)
    pm13.196a $p |- ( -. ph <-> A. y ( [ y / x ] ph -> y =/= x ) ) $=
      ( wn cv wceq wsbc wa wex wal wne sbelx sb56 sbn imbi2i con2b df-ne bicomi
      wi 3bitri albii ) ADZCEZBEZFZUBBUCGZHCIUEUFSZCJABUCGZUCUDKZSZCJUBCBLUFCBM
      UGUJCUGUEUHDZSUHUEDZSUJUFUKUEABCNOUEUHPULUIUHUIULUCUDQROTUAT $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.21 in [WhiteheadRussell] p. 179.  (Contributed by Andrew
       Salmon, 3-Jun-2011.) $)
    2sbc6g $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ( z = A /\
      w = B ) -> ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wi wal wsbc wb eqeq2 imbi1d 2albidv anbi2d dfsbcq
      ax-17 sbbid bibi12d anbi1d 2sb6 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZEL
      ZMZANZCOBOZACEPZBDPZQZUKHKZLZUMIKZLZMZANZCOBOZACVCPZBVAPZQVBUNMZANZCOBOZU
      RBVAPZQUTIHEDGFVCELZVGVLVIVMVNVFVKBCVNVEVJAVNVDUNVBVCEUMRUASTVNVHURBHVNBU
      CACVCEUBUDUEVADLZVLUQVMUSVOVKUPBCVOVJUOAVOVBULUNVADUKRUFSTURBVADUBUEVIVGA
      BCHIUGUHUIUJ $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.22 in [WhiteheadRussell] p. 179.  (Contributed by Andrew
       Salmon, 3-Jun-2011.) $)
    2sbc5g $p |- ( ( A e. C /\ B e. D ) -> ( E. z E. w ( ( z = A /\ w = B )
      /\ ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wex wsbc wb eqeq2 anbi1d 2exbidv dfsbcq bibi12d
      anbi2d ax-17 sbbid 2sb5 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZELZMZAMZCN
      BNZACEOZBDOZPZUIHKZLZUKIKZLZMZAMZCNBNZACVAOZBUSOZPUTULMZAMZCNBNZUPBUSOZPU
      RIHEDGFVAELZVEVJVGVKVLVDVIBCVLVCVHAVLVBULUTVAEUKQUBRSVLVFUPBHVLBUCACVAETU
      DUAUSDLZVJUOVKUQVMVIUNBCVMVHUMAVMUTUJULUSDUIQRRSUPBUSDTUAVGVEABCHIUEUFUGU
      H $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Equivalence between two different forms of ` iota ` .  (Contributed by
       Andrew Salmon, 15-Jul-2011.) $)
    iotain $p |- ( E! x ph -> |^| { x | ph } = ( iota x ph ) ) $=
      ( vy weu cv wceq wal wex cab cint cio df-eu csn vex intsn hba1 ax-4 abbid
      wb df-sn syl6eqr inteqd iotaval 3eqtr4a exlimiv sylbi ) ABDABECEZFZSZBGZC
      HABIZJZABKZFZABCLUJUNCUJUGMZJUGULUMUGCNOUJUKUOUJUKUHBIUOUJAUHBUIBPUIBQRBU
      GTUAUBABCUCUDUEUF $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d y ph $.  $d y x $.
    $( The iota class exists.  This theorem does not require ~ ax-nul for its
       proof.  (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    iotaexeu $p |- ( E! x ph -> ( iota x ph ) e. _V ) $=
      ( vy cv wceq wb wal wex cio weu cvv wcel iotaval eqcomd eximi df-eu isset
      3imtr4i ) ABDCDZEFBGZCHSABIZEZCHABJUAKLTUBCTUASABCMNOABCPCUAQR $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Definition *14.01 in [WhiteheadRussell] p. 184.  In Principia
       Mathematica, Russell and Whitehead define ` iota ` in terms of a
       function of ` ( iota x ph ) ` .  Their definition differs in that a
       function of ` ( iota x ph ) ` evaluates to "false" when there isn't a
       single ` x ` that satisfies ` ph ` .  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    iotasbc $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
         E. y ( A. x ( ph <-> x = y ) /\ ps ) ) ) $=
      ( weu cio wsbc cv wceq wa wex wb wal cvv wcel iotaexeu sbc5g syl wi eueq
      sylib df-eu iotaval eqcomd ancri eximi sylbi eupick syl2anc anbi1d exbidv
      impbid1 bitrd ) ACEZBDACFZGZDHZUOIZBJZDKZACHUQILCMZBJZDKUNUONOZUPUTLACPZB
      DUONQRUNUSVBDUNURVABUNURVAUNURDEZURVAJZDKZURVASUNVCVEVDDUOTUAUNVADKVGACDU
      BVAVFDVAURVAUOUQACDUCUDZUEUFUGURVADUHUIVHULUJUKUM $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph y z $.  $d ps y z $.
    $( Theorem *14.111 in [WhiteheadRussell] p. 184.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotasbc2 $p |- ( ( E! x ph /\ E! x ps ) -> ( [ ( iota x ph ) / y ]
        [ ( iota x ps ) / z ] ch <-> E. y E. z ( A. x ( ph <-> x = y ) /\
        A. x ( ps <-> x = z ) /\ ch ) ) ) $=
      ( weu cio wsbc cv wceq wb wal wa wex w3a iotasbc anbi2d 3anass exbii
      19.42v bitr2i syl6bb exbidv sylan9bb ) ADGCFBDHIZEADHIADJZEJKLDMZUFNZEOBD
      GZUHBUGFJKLDMZCPZFOZEOAUFDEQUJUIUMEUJUIUHUKCNZFOZNZUMUJUFUOUHBCDFQRUMUHUN
      NZFOUPULUQFUHUKCSTUHUNFUAUBUCUDUE $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d ph y $.  $d x y $.
    $( Theorem *14.12 in [WhiteheadRussell] p. 184.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    pm14.12 $p |- ( E! x ph -> A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) )
        $=
      ( weu wmo cv wsbc wa wceq wi wal eumo ax-17 mo3 sylib ) ABDABEAABCFZGHBFP
      IJCKBKABLABCACMNO $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d y x A $.  $d y B $.  $d ph y $.
    $( Theorem *14.122 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.122a $p |- ( A e. V -> ( A. x ( ph <-> x = A ) <-> ( A. x ( ph ->
      x = A ) /\ [ A / x ] ph ) ) ) $=
      ( cv wceq wb wal wi wa wcel wsbc albiim sbc6g bicomd anbi2d syl5bb ) ABEC
      FZGBHARIBHZRAIBHZJCDKZSABCLZJARBMUATUBSUAUBTABCDNOPQ $.
      $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.122b $p |- ( A e. V -> ( ( A. x ( ph -> x = A ) /\ [ A / x ] ph ) <->
      ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( vy wcel cv wceq wi wal wex wb eqeq2 imbi2d albidv dfsbcq bibi1d imbi12d
      wsbc wa sb5 hba1 simpr ancr a4s impbid2 exbid syl5bb vtoclg pm5.32d ) CDF
      ABGZCHZIZBJZABCSZABKZAUKEGZHZIZBJZABUQSZUPLZIUNUOUPLZIECDUQCHZUTUNVBVCVDU
      SUMBVDURULAUQCUKMNOVDVAUOUPABUQCPQRVAURATZBKUTUPABEUAUTVEABUSBUBUTVEAURAU
      CUSAVEIBAURUDUEUFUGUHUIUJ $.
      $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.122c $p |- ( A e. V -> ( A. x ( ph <-> x = A ) <->
                    ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( wcel cv wceq wb wal wi wsbc wa wex pm14.122a pm14.122b bitrd ) CDEABFCG
      ZHBIAQJBIZABCKLRABMLABCDNABCDOP $.
      $( [9-Jun-2011] $)
  $}

  ${
    $d w A z $.  $d w B z $.
    $( Theorem *14.123 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.123a $p |- ( ( A e. V /\ B e. W ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      [ A / z ] [ B / w ] ph ) ) ) $=
      ( cv wceq wa wb wal wi wcel wsbc 2albiim 2sbc6g anbi2d syl5bb ) ABHDICHEI
      JZKCLBLATMCLBLZTAMCLBLZJDFNEGNJZUAACEOBDOZJATBCPUCUBUDUAABCDEFGQRS $.
      $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.123b $p |- ( ( A e. V /\ B e. W ) -> ( ( A. z A. w ( ph ->
      ( z = A /\ w = B ) ) /\ [ A / z ] [ B / w ] ph ) <->
      ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\ E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wi wal wsbc wex wb 2sbc5g adantr hba1 exbid simpr ancrd
      hba2 ax-4 a4s impbid2 adantl bitr3d pm5.32da ) DFHEGHIZABJDKCJEKIZLZCMZBM
      ZACENBDNZACOZBOZUJUNIUKAIZCOZBOZUOUQUJUTUOPUNABCDEFGQRUNUTUQPUJUNUSUPBUMB
      SUNURACULCBUCUNURAUKAUAUNAUKUMULBULCUDUEUBUFTTUGUHUI $.
      $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185.  (Contributed by Andrew
       Salmon, 9-Jun-2011.) $)
    pm14.123c $p |- ( ( A e. V /\ B e. W ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wb wal wi wsbc wex pm14.123a pm14.123b bitrd ) DFHEGHIA
      BJDKCJEKIZLCMBMATNCMBMZACEOBDOIUAACPBPIABCDEFGQABCDEFGRS $.
      $( [9-Jun-2011] $)
  $}

  ${
    $( Theorem *14.18 in [WhiteheadRussell] p. 189.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    pm14.18 $p |- ( E! x ph -> ( A. x ps -> [ ( iota x ph ) / x ] ps ) ) $=
      ( weu cio cvv wcel wal wsbc wi iotaexeu a4sbc syl ) ACDACEZFGBCHBCNIJACKB
      CNFLM $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.
    $( Theorem *14.2 in [WhiteheadRussell] p. 189.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iota2 $p |- ( iota x x = y ) = y $=
      ( cv wceq wb cio iotaval biid mpg ) ACBCZDZKEKAFJDAKABGKHI $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    $( Theorem *14.202 in [WhiteheadRussell] p. 189.  A biconditional version
       of ~ iotaval .  (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    iotavalb $p |- ( E! x ph -> ( A. x ( ph <-> x = y ) <->
        ( iota x ph ) = y ) ) $=
      ( vz weu cv wceq wb wal cio iotaval wa wex wsbc iotasbc cvv wcel iotaexeu
      eqsbc3 syl bitr3d equequ2 bibi2d albidv biimpac exlimiv syl6bir impbid2 )
      ABEZABFZCFZGZHZBIZABJZUKGZABCKUIUPAUJDFZGZHZBIZUQUKGZLZDMZUNUIVADUONZVCUP
      AVABDOUIUOPQVDUPHABRDUOUKPSTUAVBUNDVAUTUNVAUSUMBVAURULADCBUBUCUDUEUFUGUH
      $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.205 in [WhiteheadRussell] p. 190.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotasbc5 $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
        E. y ( y = ( iota x ph ) /\ ps ) ) ) $=
      ( weu cio cvv wcel wsbc cv wceq wa wex wb iotaexeu sbc5g syl ) ACEACFZGHB
      DRIDJRKBLDMNACOBDRGPQ $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d z y x $.  $d z y ph $.
    $( Theorem *14.24 in [WhiteheadRussell] p. 191.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    pm14.24 $p |- ( E! x ph -> A. y ( [ y / x ] ph <-> y = ( iota x ph ) ) ) $=
      ( weu cv wsbc cio wceq wb wal hbeu1 hbs1 pm14.12 19.21bbi ancomsd expdimp
      wa wi sbequ2 impbid com12 adantl ex alrimd iotaval eqcomd syl6 syl5ibrcom
      iota4 dfsbcq alrimiv ) ABDZABCEZFZUMABGZHZICULUNUPULUNABEUMHZIZBJZUPULUNU
      RBABKABCLULUNURULUNQAUQULUNAUQULAUNUQULAUNQUQRBCABCMNOPUNUQARULUQUNAABCSU
      AUBTUCUDUSUOUMABCUEUFUGULUNUPABUOFABUIABUMUOUJUHTUK $.
      $( [12-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.242 in [WhiteheadRussell] p. 192.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotavalsb $p |- ( A. x ( ph <-> x = y ) -> ( [ y / z ] ps <->
        [ ( iota x ph ) / z ] ps ) ) $=
      ( cv wceq wb wal wex cio 19.8a weu wi df-eu iotavalb dfsbcq eqcoms syl6bi
      wsbc sylbir mpcom ) ACFDFZGHCIZDJZUDBEUCTBEACKZTHZUDDLUEACMZUDUGNACDOUHUD
      UFUCGUGACDPUGUCUFBEUCUFQRSUAUB $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Theorem *14.25 in [WhiteheadRussell] p. 192.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    sbiota1 $p |- ( E! x ph -> ( A. x ( ph -> ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( vy weu wi wal cio wsbc cv wceq wb wex df-eu biimpi iota4 dfsbcq sylc wa
      cvv iotaval eqcomd a4sbim imbi12d com23 syl exlimiv wcel iotaexeu anbi12d
      syl5ib imbi1d sban a4sbe sylbir vtoclg exp3a anc2li eupicka syl6 impbid )
      ACEZABFCGZBCACHZIZVBACJDJZKLCGZDMZACVDIZVCVEFZVBVHACDNOACPZVGVIVJFZDVGVFV
      DKZVLVGVDVFACDUAUBVMVCVIVEVCACVFIZBCVFIZFVMVIVEFABCDUCVMVNVIVOVEACVFVDQZB
      CVFVDQZUDUKUEUFUGRVBVEVBABSZCMZSVCVBVEVSVBVDTUHZVIVEVSFACUIVKVTVIVEVSVNVO
      SZVSFVIVESZVSFDVDTVMWAWBVSVMVNVIVOVEVPVQUJULWAVRCVFIVSABCDUMVRCDUNUOUPUQR
      URABCUSUTVA $.
      $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.26 in [WhiteheadRussell] p. 192.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    sbaniota $p |- ( E! x ph -> ( E. x ( ph /\ ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( weu wa wex wi wal cio wsbc eupickbi sbiota1 bitrd ) ACDABECFABGCHBCACIJ
      ABCKABCLM $.
      $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.271 in [WhiteheadRussell] p. 192.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    eubi $p |- ( A. x ( ph <-> ps ) -> ( E! x ph <-> E! x ps ) ) $=
      ( wb wal hba1 ax-4 eubid ) ABDZCEABCICFICGH $.
      $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem *14.272 in [WhiteheadRussell] p. 193.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotasbcq $p |- ( A. x ( ph <-> ps ) -> ( [ ( iota x ph ) / y ] ch <->
        [ ( iota x ps ) / y ] ch ) ) $=
      ( wb wal cio wceq wsbc iotabi dfsbcq syl ) ABFDGADHZBDHZICENJCEOJFABDKCEN
      OLM $.
      $( [11-Jul-2011] $)
  $}

