$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Group homomorphism and isomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B y $.  $d F w x y z $.  $d G x y $.  $d H w x y z $.
    $d M y z $.  $d S x y $.  $d T x y $.  $d X w x y z $.  $d Z w x y z $.
    ghomgrpilem1.1 $e |- G e. GrpOp $.
    ghomgrpilem1.2 $e |- H e. GrpOp $.
    ghomgrpilem1.3 $e |- F e. ( G GrpOpHom H ) $.
    ghomgrpilem1.4 $e |- X = ran G $.
    ghomgrpilem1.5 $e |- U = ( Id ` G ) $.
    ghomgrpilem1.6 $e |- N = ( inv ` G ) $.
    ghomgrpilem1.7 $e |- W = ran H $.
    ghomgrpilem1.8 $e |- T = ( Id ` H ) $.
    ghomgrpilem1.9 $e |- M = ( inv ` H ) $.
    ghomgrpilem1.10 $e |- Z = ran F $.
    ghomgrpilem1.11 $e |- S = ( H |` ( Z X. Z ) ) $.
    $( Lemma for ~ ghomgrpi . $)
    ghomgrpilem1 $p |- ( ( A e. X /\ B e. X ) ->
                         ( ( F ` A ) H ( F ` B ) ) = ( F ` ( A G B ) ) ) $=
      ( vy vx wcel cv co wceq wral fveq2 oveq1d oveq1 fveq2d eqeq12d ralbidv wf
      cfv cghom wa cgr wb elghom mp2an simpri vtoclri oveq2d oveq2 rcla4v mpan9
      mpbi ) ALUGAFUSZUEUHZFUSZHUIZAVNGUIZFUSZUJZUELUKZBLUGVMBFUSZHUIZABGUIZFUS
      ZUJZUFUHZFUSZVOHUIZWFVNGUIZFUSZUJZUELUKZVTUFALWFAUJZWKVSUELWMWHVPWJVRWMWG
      VMVOHWFAFULUMWMWIVQFWFAVNGUNUOUPUQLKFURZWLUFLUKZFGHUTUIUGZWNWOVAZPGVBUGHV
      BUGWPWQVCNOUFUEFGHKLQTVDVEVLVFVGVSWEUEBLVNBUJZVPWBVRWDWRVOWAVMHVNBFULVHWR
      VQWCFVNBAGVIUOUPVJVK $.
      $( [25-Feb-2008] $)

    $( Lemma for ~ ghomgrpi . $)
    ghomgrpilem2 $p |- S e. ( SubGrpOp ` H ) $=
      ( vx vy vz vw crn wf wss cv cfv co wceq wral cghom wa cgr wb elghom mp2an
      wcel mpbi simpli frn ax-mp eqsstri wrex eleq2i wfn fvelrnb biimpi anim12i
      ffn bitri reeanv sylibr ghomgrpilem1 grpocl dffn3 feq3 mpbir ffvelrni syl
      mp3an1 eqeltrd oveq12 eleq1d syl5ibcom rexlimivv cgi grpolid fveq2i eqtri
      grpoidcl ffvelrn eqid grpoid eqtr4i eqeltri grpoinvcl mpan mpdan grporinv
      fveq2d eqtrd syl6eqr grpoinvid1 syl2anc mpbird fveq2 rexlimiv issubgoi
      sylbi ) UCUDBFAGIKMRSTKDUGZIUAJIDUHZXNIUIXOUCUJZDUKUDUJZDUKFULXPXQEULDUKU
      MUDJUNUCJUNZDEFUOULVAZXOXRUPZNEUQVAZFUQVAZXSXTURLMUCUDDEFIJORUSUTVBVCZJID
      VDVEVFUBXPKVAZXQKVAZUPZUEUJZDUKZXPUMZUFUJZDUKZXQUMZUPZUFJVGUEJVGZXPXQFULZ
      KVAZYFYIUEJVGZYLUFJVGZUPYNYDYQYEYRYDYQYDXPXNVAZYQKXNXPUAVHDJVIZYSYQURXOYT
      YCJIDVMVEZUEJXPDVJVEVNZVKYEYRYEXQXNVAZYRKXNXQUAVHYTUUCYRURUUAUFJXQDVJVEVN
      VKVLYIYLUEUFJJVOVPYMYPUEUFJJYGJVAZYJJVAZUPZYHYKFULZKVAYMYPUUFUUGYGYJEULZD
      UKZKYGYJABCDEFGHIJKLMNOPQRSTUAUBVQUUFUUHJVAZUUIKVAYAUUDUUEUUJLYGYJEJOVRWD
      JKUUHDJKDUHZJXNDUHZYTUULUUAJDVSVBKXNUMUUKUULURUAKXNJDVTVEWAZWBWCWEYMUUGYO
      KYHXPYKXQFWFWGWHWIWCBCDUKZKBFWJUKZUUNSUUNUUOUMZUUNUUNFULZUUNUMZUUQCCEULZD
      UKZUUNCJVAZUVAUUQUUTUMYAUVALCEJOPWNVEZUVBCCABCDEFGHIJKLMNOPQRSTUAUBVQUTUU
      SCDYAUVAUUSCUMLUVBCCEJOPWKUTWLWMYBUUNIVAZUUPUURURMXOUVAUVCYCUVBJICDWOUTUU
      NUUOFIRUUOWPWQUTWAWRZUUKUVAUUNKVAUUMUVBJKCDWOUTWSYDYQXPGUKZKVAZUUBYIUVFUE
      JUUDYHGUKZKVAYIUVFUUDUVGYGHUKZDUKZKUUDUVGUVIUMZYHUVIFULZBUMZUUDUVKUUNBUUD
      UVKYGUVHEULZDUKZUUNUUDUVHJVAZUVKUVNUMYAUUDUVOLYGEHJOQWTXAZYGUVHABCDEFGHIJ
      KLMNOPQRSTUAUBVQXBUUDUVMCDYAUUDUVMCUMLYGCEHJOPQXCXAXDXEUVDXFUUDYHIVAZUVII
      VAZUVJUVLURZJIYGDYCWBUUDUVOUVRUVPJIUVHDYCWBWCYBUVQUVRUVSMYHUVIBFGIRSTXGWD
      XHXIUUDUVOUVIKVAUVPJKUVHDUUMWBWCWEYIUVGUVEKYHXPGXJWGWHXKXMXL $.
      $( [25-Feb-2008] $)
  $}

  ${
    ghomgrpi.1 $e |- G e. GrpOp $.
    ghomgrpi.2 $e |- H e. GrpOp $.
    ghomgrpi.3 $e |- F e. ( G GrpOpHom H ) $.
    ghomgrpi.4 $e |- Y = ran F $.
    ghomgrpi.5 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( The image of a group homomorphism from ` G ` to ` H ` is a subgroup of
       ` H ` (inference version).  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    ghomgrpi $p |- S e. ( SubGrpOp ` H ) $=
      ( cgi cfv cgn crn eqid ghomgrpilem2 ) ADKLZCKLZBCDDMLZCMLZDNZCNZEFGHUBORO
      TOUAOQOSOIJP $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d G x y $.
    ghomsn.1 $e |- A e. _V $.
    ghomsn.2 $e |- G = { <. <. A , A >. , A >. } $.
    $( The endomorphism of the trivial group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    ghomsn $p |- ( _I |` { A } ) e. ( G GrpOpHom G ) $=
      ( vx vy csn co wcel cv wceq wral ax-mp wa elsn fveq2 fvresi syl6eq eqtr4d
      cfv cid cres cghom wf wf1o f1oi f1of snid oveqan12d oveq12 syl2anb grposn
      cgr cop eqeltri crn opex rnsnop eqtr2i grpocl mp3an1 syl rgen2a wb elghom
      rneqi mp2an mpbir2an ) UAAGZUBZBBUCHIZVIVIVJUDZEJZVJTZFJZVJTZBHZVMVOBHZVJ
      TZKZFVILEVILZVIVIVJUEVLVIUFVIVIVJUGMVTEFVIVMVIIZVOVIIZNZVQVRVSWBVMAKZVOAK
      ZVQVRKWCEAOFAOWEWFNVQAABHVRWEWFVNAVPABWEVNAVJTZAVMAVJPAVIIWGAKACUHVIAQMZR
      WFVPWGAVOAVJPWHRUIVMAVOABUJSUKWDVRVIIZVSVRKBUMIZWBWCWIBAAUNZAUNGZUMDACULU
      OZVMVOBVIBUPWLUPVIBWLDVFWKAAAUQCURUSZUTVAVIVRQVBSVCWJWJVKVLWANVDWMWMEFVJB
      BVIVIWNWNVEVGVH $.
      $( [25-Feb-2008] $)
  $}

  ${
    ghomgrplem.1 $e |- ( ph ->
                      ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) )
        $.
    ghomgrplem.2 $e |- S = { <. <. z , z >. , z >. } $.
    ghomgrplem.3 $e |- J = ( _I |` { z } ) $.
    $( Lemma for ~ ghomgrp . $)
    ghomgrplem $p |- ( ph
            -> ( H |` ( ran F X. ran F ) ) e. ( SubGrpOp ` H ) ) $=
      ( crn cxp cres csubgo wcel cif wceq cgr cghom eleq1 cfv reseq1 fveq2 rneq
      eleq12d xpeq1 xpeq2 eqtrd reseq2d syl eleq1d co simp1d cop csn vex grposn
      eqeltri elimdhyp simp2d simp3d cid ghomsn elimdelov eqid ghomgrpi dedth2v
      cv ) AFDKZVILZMZFNUAZOAFCPZVJMZVMNUAZOVMADGPZKZVQLZMZVOOFDCGFVMQVKVNVLVOF
      VMVJUBFVMNUCUEDVPQZVNVSVOVTVIVQQZVNVSQDVPUDWAVJVRVMWAVJVQVILVRVIVQVIUFVIV
      QVQUGUHUIUJUKVSVPAECPZVMVQAEROZWBROCROZECAWCFROZDEFSULOZHUMEWBRTCWBRTCBVH
      ZWGUNWGUNUORIWGBUPZUQURZUSAWEVMROWDFCAWCWEWFHUTFVMRTCVMRTWIUSAEFDSCCGAWCW
      EWFHVAGVBWGUOMCCSULJWGCWHIVCURVDVQVEVSVEVFVG $.
      $( [25-Feb-2008] $)
  $}

  ${
    ghomgrp.1 $e |- Y = ran F $.
    ghomgrp.2 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( The image of a group homomorphism from ` G ` to ` H ` is a subgroup of
       ` H ` .  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    ghomgrp $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) ->
                     S e. ( SubGrpOp ` H ) ) $=
      ( vx cgr wcel cghom co w3a crn cxp cres wceq cop csn eqid cfv ax-mp eqtri
      csubgo xpid11 reseq2 sylbir cv cid id ghomgrplem syl5eqel ) CIJDIJBCDKLJM
      ZADBNZUNOZPZDUDUAADEEOZPZUPGEUNQZURUPQZFUSUQUOQUTEUNUEUQUODUFUGUBUCUMHHUH
      ZVARVARSZBCDUIVASPZUMUJVBTVCTUKUL $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomfo.1 $e |- X = ran G $.
    ghomfo.2 $e |- Y = ran F $.
    ghomfo.3 $e |- S = ( H |` ( Y X. Y ) ) $.
    ghomfo.4 $e |- Z = ran S $.
    $( A group homomorphism maps onto its image.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomfo $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) ->
                   F : X -onto-> Z ) $=
      ( vx vy cgr wcel co wceq wfo cfv wss cghom w3a wfn crn wf cv wral wa eqid
      elghom biimp3a simpld ffn syl cxp cres dmeqi csubgo ghomgrp issubgo sylib
      cdm simp2d grpofo fof fdm syl5reqr frn syl5eqss xpss12 syl2anc wb ssdmres
      3syl sseq2d syl5rbbr 3ad2ant2 mpbid eqtrd xpid11 syl6req df-fo sylanbrc )
      CNOZDNOZBCDUAPOZUBZBEUCZBUDZGQEGBRWGEDUDZBUEZWHWGWKLUFZBSMUFZBSDPWLWMCPBS
      QMEUGLEUGZWDWEWFWKWNUHLMBCDWJEHWJUIZUJUKULZEWJBUMUNWGGFWIWGGGUOZFFUOZQGFQ
      WGWQDWRUPZVBZWRWGWTAVBZWQAWSJUQWGANOZXAWQQZWGWEXBADTZWGADURSOWEXBXDUBABCD
      FIJUSDAUTVAVCXBWQGARWQGAUEXCAGKVDWQGAVEWQGAVFVNUNVGWGWRWJWJUOZTZWTWRQZWGF
      WJTZXHXFWGFWIWJIWGWKWIWJTWPEWJBVHUNVIZXIFWJFWJVJVKWEWDXFXGVLWFXGWRDVBZTWE
      XFWRDVMWEXJXEWRWEXEWJDRXEWJDUEXJXEQDWJWOVDXEWJDVEXEWJDVFVNVOVPVQVRVSGFVTV
      AIWAEGBWBWC $.
      $( [3-Mar-2008] $)

    $( Closure of a group homomorphism.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomcl $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) ->
                   ( A e. X -> ( F ` A ) e. Z ) ) $=
      ( cgr wcel cghom co w3a wfo wf cfv wi ghomfo fof ffvelrn ex 3syl ) DMNEMN
      CDEOPNQFHCRFHCSZAFNZACTHNZUABCDEFGHIJKLUBFHCUCUGUHUIFHACUDUEUF $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d S x y $.
    ghomgsg.1 $e |- Y = ran F $.
    ghomgsg.2 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( A group homomorphism from ` G ` to ` H ` is also a group homomorphism
       from ` G ` to its image in ` H ` .  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomgsg $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) ->
                    F e. ( G GrpOpHom S ) ) $=
      ( vx vy cgr wcel cghom co w3a crn cfv wceq wral eqid wa wf wfo ghomfo fof
      syl elghom biimp3a simprd ffvelrn anim12dan csubgo ghomgrp subgoov syldan
      cv sylan eqeq1d 2ralbidva mpbird wss issubgo sylib simp2d biimprd 3adant3
      wi syld3an2 mp2and ) CJKZDJKZBCDLMKZNZCOZAOZBUAZHUOZBPZIUOZBPZAMZVPVRCMBP
      ZQZIVMRHVMRZBCALMKZVLVMVNBUBVOABCDVMEVNVMSZFGVNSZUCVMVNBUDUEZVLWCVQVSDMZW
      AQZIVMRHVMRZVLVMDOZBUAZWJVIVJVKWLWJTHIBCDWKVMWEWKSUFUGUHVLWBWIHIVMVMVLVPV
      MKZVRVMKZTZTVTWHWAVLWOVQVNKZVSVNKZTZVTWHQZVLVOWOWRWGVOWMWPWNWQVMVNVPBUIVM
      VNVRBUIUJUPVLADUKPKZWRWSABCDEFGULZVQVSDAVNWFUMUPUNUQURUSVIAJKZVJVKVOWCTZW
      DVFZVLVJXBADUTZVLWTVJXBXENXADAVAVBVCVIXBXDVKVIXBTWDXCHIBCAVNVMWEWFUFVDVEV
      GVH $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F x $.  $d G x $.  $d H x $.  $d T x $.  $d U x $.  $d X x $.  $d Z x $.
    ghomf1olem.1 $e |- X = ran G $.
    ghomf1olem.2 $e |- Y = ran F $.
    ghomf1olem.3 $e |- S = ( H |` ( Y X. Y ) ) $.
    ghomf1olem.4 $e |- Z = ran S $.
    ghomf1olem.5 $e |- U = ( Id ` G ) $.
    ghomf1olem.6 $e |- T = ( Id ` H ) $.

    ${
      $d F x y z $.  $d G y z $.  $d H y z $.  $d N x $.  $d T y z $.
      $d U y z $.  $d X y z $.
      ghomf1olem.7 $e |- N = ( inv ` G ) $.
      $( Lemma for ~ ghomf1o . $)
      ghomf1olem $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) )
          ->
          ( F : X -1-1-onto-> Z <-> A. x e. X ( ( F ` x ) = T -> x = U ) ) ) $=
        ( wceq wi vy vz cgr wcel cghom co w3a wf1o cv cfv wf wf1 wa f1of1 dff13
        sylib simprd grpoidcl fveq2 eqeq1d equequ1 imbi12d eqeq2d eqeq2 rcla42v
        wral expcom syl com23 3ad2ant1 ghomid imbi1d imbi2d sylibd ralrimdv wfo
        ghomfo adantr fof grpoinvcl 3adant2 grpocl syld3an3 3expib eqeq1 rcla4v
        syl5 imp oveq1 3ad2ant3 simprl 3ad2antl1 adantrl ghomlin syldan 3adant3
        syl6 jca simprr grporinv fveq2d 3eqtrd 3eqtr3d 3expia equcom grpoinvid2
        grpo2inv 3com23 bitr3d syl5bbr 3expb biimprd imim12d impancom ralrimivv
        wb syld sylanbrc df-f1o ex impbid ) FUCUDZGUCUDZEFGUEUFUDZUGZIKEUHZAUIZ
        EUJZCSZYGDSZTZAIVFZYEYFYKAIYEYFYGIUDZYHDEUJZSZYJTZTZYMYKTYFUAUIZEUJZUBU
        IZEUJZSZYRYTSZTZUBIVFUAIVFZYEYQYFIKEUKZUUEYFIKEULZUUFUUEUMIKEUNUAUBIKEU
        OZUPUQYBYCUUEYQTYDYBYMUUEYPYBDIUDZYMUUEYPTZTDFILPURYMUUIUUJUUDYPYHUUASZ
        YGYTSZTUAUBYGDIIYRYGSZUUBUUKUUCUULUUMYSYHUUAYRYGEUSUTUAAUBVAVBYTDSZUUKY
        OUULYJUUNUUAYNYHYTDEUSVCYTDYGVDVBVEVGVHVIVJWGYEYPYKYMYEYOYIYJYEYNCYHCDE
        FGPQVKZVCVLVMVNVOYEYLYFYEYLUMZUUGIKEVPZYFUUPUUFUUEUUGUUPUUQUUFYEUUQYLBE
        FGIJKLMNOVQVRZIKEVSVHUUPUUDUAUBIIYEYRIUDZYTIUDZUMZYLUUDYEUVAUMZYLYRYTHU
        JZFUFZEUJZCSZUVDDSZTZUUDYEUVAYLUVHTZYEUVAUVDIUDZUVIYBYCUVAUVJTYDYBUUSUU
        TUVJYBUUSUUTUVCIUDZUVJYBUUTUVKUUSYTFHILRVTZWAZYRUVCFILWBWCWDVJYKUVHAUVD
        IYGUVDSZYIUVFYJUVGUVNYHUVECYGUVDEUSUTYGUVDDWEVBWFWQWHUVBUUBUVFUVGUUCYEU
        VAUUBUVFYEUVAUUBUGYSUVCEUJZGUFZUUAUVOGUFZUVECUUBYEUVPUVQSUVAYSUUAUVOGWI
        WJYEUVAUVPUVESZUUBYEUVAUUSUVKUMUVRUVBUUSUVKYEUUSUUTWKYEUUTUVKUUSYBYCUUT
        UVKYDUVLWLWMZWRYRUVCEFGILWNWOWPYEUVAUVQCSUUBUVBUVQYTUVCFUFZEUJZYNCYEUVA
        UUTUVKUMUVQUWASUVBUUTUVKYEUUSUUTWSUVSWRYTUVCEFGILWNWOUVBUVTDEYEUUTUVTDS
        ZUUSYBYCUUTUWBYDYTDFHILPRWTWLWMXAYEYNCSUVAUUOVRXBWPXCXDUVBUUCUVGYBYCUVA
        UUCUVGXPZYDYBUUSUUTUWCUUCYTYRSZYBUUSUUTUGZUVGUBUAXEUWEUVCHUJZYRSZUWDUVG
        UWEUWFYTYRYBUUTUWFYTSUUSYTFHILRXGWAUTYBUUSUUTUVKUWGUVGXPZUVMYBUVKUUSUWH
        UVCYRDFHILPRXFXHWCXIXJXKWLXLXMXQXNXOUUHXRUURIKEXSXRXTYA $.
        $( [3-Mar-2008] $)
    $}

    $( Two ways of saying a group homomorphism is 1-1-onto its image.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    ghomf1o $p |- ( ( G e. GrpOp /\ H e. GrpOp /\ F e. ( G GrpOpHom H ) ) ->
        ( F : X -1-1-onto-> Z <-> A. x e. X ( ( F ` x ) = T -> x = U ) ) ) $=
      ( cgn cfv eqid ghomf1olem ) ABCDEFGFQRZHIJKLMNOPUAST $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F f $.  $d G f g h s $.  $d H f g h s $.
    $( Membership in the set of group isomorphisms from ` G ` to ` H ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elgiso $p |- ( ( G e. GrpOp /\ H e. GrpOp ) -> ( F e. ( G GrpOpIso H ) <->
        ( F e. ( G GrpOpHom H ) /\ F : ran G -1-1-onto-> ran H ) ) ) $=
      ( vf vg vh vs cgr wcel wa cgiso co crn cv wf1o cghom crab wceq wb rneq
      rabex oveq1 f1oeq2 syl rabeqbidv oveq2 f1oeq3 df-giso eleq2d f1oeq1 elrab
      ovex ov2 syl6bb ) BHICHIJZABCKLZIABMZCMZDNZOZDBCPLZQZIAVAIUQURAOZJUOUPVBA
      EFGBCHHENZMZFNZMZUSOZDVDVFPLZQVBKUQVGUSOZDBVFPLZQUTDVABCPULUAVDBRZVHVJDVI
      VKVDBVFPUBVLVEUQRVHVJSVDBTVEUQVGUSUCUDUEVFCRZVJUTDVKVAVFCBPUFVMVGURRVJUTS
      VFCTVGURUQUSUGUDUEDEFGUHUMUIUTVCDAVAUQURUSAUJUKUN $.
      $( [25-Feb-2008] $)
  $}

