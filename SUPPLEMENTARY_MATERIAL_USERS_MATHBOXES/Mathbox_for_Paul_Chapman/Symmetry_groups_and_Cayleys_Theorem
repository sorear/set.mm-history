$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( The symmetry group on ` A ` is a group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    symggrp $p |- ( A e. V -> ( SymGrp ` A ) e. GrpOp ) $=
      ( vx cv csymg cfv cgr wcel wceq fveq2 eleq1d vex symggrpi vtoclg ) CDZEFZ
      GHAEFZGHCABOAIPQGOAEJKOCLMN $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d A a b f g h $.  $d F f w x y z $.  $d G a b c f g h w x y z $.
    $d H w x y z $.  $d P c g h $.  $d S y $.  $d U a b y $.
    $d X a b c f g h w x y z $.  $d Y y $.
    cayleylem1.1 $e |- G e. GrpOp $.
    cayleylem1.2 $e |- P = { f | f : X -1-1-onto-> X } $.
    cayleylem1.3 $e |- X = ran G $.
    cayleylem1.4 $e |- U = ( Id ` G ) $.
    cayleylem1.5 $e |- H = ( SymGrp ` X ) $.
    cayleylem1.6 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                             ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleylem1.7 $e |- Y = ran F $.
    cayleylem1.8 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Lemma for ~ cayleyi . $)
    cayleylem1 $p |- ( A e. X -> ( F ` A ) e. P ) $=
      ( wcel cfv wf1o cgr grpolactf1o mpan crn cvv rnexg ax-mp eqeltri elsymgrn
      sylibr ) AKUCZKKAHUDZUEZUQBUCIUFUCZUPUROAFGHIKMNTQUGUHEKBUQKIUIZUJQUSUTUJ
      UCOIUFUKULUMPUNUO $.
      $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem2 $p |- F e. ( G GrpOpHom H ) $=
      ( vx vy vc vz vw cghom co wcel wf cv cfv wceq wral cgr wa csymg crn rnexg
      wb cvv ax-mp eqeltri symggrpi symgf fdmi grporn rneqi eqtr4i elghom mp2an
      cxp copab grplactfval mpan cayleylem1 eqeltrrd rgen oveq1 eqeq2d opabbidv
      anbi2d eleq1d cbvralv fopab2 bitri mpbi ccom symgov syl2an syl5eq grpoass
      oveqi grpocl mp3an1 grplactval sylan 3impa wf1o elsymgrn sylib f1of fvco3
      w3a syl 3adant1 fveq2d sylan2 3eqtrd 3eqtr4rd 3expia ralrimiv wfn wss ffn
      3impb 3syl fnco syl3an 3anidm23 eqfnfv2 syl2anc mpbird eqtrd rgen2a fveq2
      frn oveq1d eqeq12d oveq2d oveq2 cbvral2v mpbir2an ) GHIUGUHUIZJAGUJZUBUKZ
      GULZUCUKZGULZIUHZYPYRHUHZGULZUMZUCJUNUBJUNZHUOUIZIUOUIYNYOUUDUPUTNIJUQULZ
      UORJJHURZVAPUUEUUGVAUINHUOUSVBVCZVDZVCUBUCGHIAJPAUUFURIURUUFAUUIAAVLAUUFD
      JAUUHOVEVFVGIUUFRVHVIVJVKLUKZJUIZMUKZUDUKZUUJHUHZUMZUPZLMVMZAUIZUDJUNZYOU
      URUDJUUMJUIZUUMGULZUUQAUUEUUTUVAUUQUMNUUMEFGHJLMSPVNVOUUMABCDEFGHIJKLMNOP
      QRSTUAVPVQVRUUSUUKUULEUKZUUJHUHZUMZUPZLMVMZAUIZEJUNYOUURUVGUDEJUUMUVBUMZU
      UQUVFAUVHUUPUVELMUVHUUOUVDUUKUVHUUNUVCUULUUMUVBUUJHVSVTWBWAWCWDEFJAUVFGSW
      EWFWGUEUKZGULZUFUKZGULZIUHZUVIUVKHUHZGULZUMZUFJUNUEJUNUUDUVPUEUFJUVIJUIZU
      VKJUIZUPZUVMUVJUVLWHZUVOUVSUVMUVJUVLUUFUHZUVTIUUFUVJUVLRWMUVQUVJAUIZUVLAU
      IZUWAUVTUMUVRUVIABCDEFGHIJKLMNOPQRSTUAVPZUVKABCDEFGHIJKLMNOPQRSTUAVPZDJAU
      VJUVLUUHOWIWJWKUVSUVTUVOUMZYPUVTULZYPUVOULZUMZUBJUNZUVSUWIUBJUVQUVRYPJUIZ
      UWIUVQUVRUWKXDZUVNYPHUHZUVIUVKYPHUHZHUHZUWHUWGUUEUWLUWMUWOUMNUVIUVKYPHJPW
      LVOUVQUVRUWKUWHUWMUMZUVSUVNJUIZUWKUWPUUEUVQUVRUWQNUVIUVKHJPWNWOZUUEUWQUWK
      UWPNUVNYPEFGHJLMSPWPWOWQWRUWLUWGYPUVLULZUVJULZUWNUVJULZUWOUVRUWKUWGUWTUMZ
      UVQUVRJJUVLUJZUWKUXBUVRJJUVLWSZUXCUVRUWCUXDUWEDJAUVLUUHOWTXAZJJUVLXBZXEZJ
      JYPUVJUVLXCWQXFUVRUWKUWTUXAUMUVQUVRUWKUPZUWSUWNUVJUUEUVRUWKUWSUWNUMNUVKYP
      EFGHJLMSPWPWOXGXFUVQUVRUWKUXAUWOUMZUXHUVQUWNJUIZUXIUUEUVRUWKUXJNUVKYPHJPW
      NWOUUEUVQUXJUXINUVIUWNEFGHJLMSPWPWOXHXPXIXJXKXLUVSUVTJXMZUVOJXMZUWFUWJUTU
      VQUVRUXKUVQUVJJXMZUVRUVLJXMZUVRUVLURJXNZUXKUVQJJUVJWSZJJUVJUJUXMUVQUWBUXP
      UWDDJAUVJUUHOWTXAJJUVJXBJJUVJXOXQUVRUXCUXNUXGJJUVLXOXEUVRUXDUXCUXOUXEUXFJ
      JUVLYGXQJJUVJUVLXRXSXTUVSUWQUXLUWRUWQJJUVOWSZJJUVOUJUXLUWQUVOAUIUXQUVNABC
      DEFGHIJKLMNOPQRSTUAVPDJAUVOUUHOWTXAJJUVOXBJJUVOXOXQXEUBJUVTUVOYAYBYCYDYEU
      VPUUCYQUVLIUHZYPUVKHUHZGULZUMUEUFUBUCJJUVIYPUMZUVMUXRUVOUXTUYAUVJYQUVLIUV
      IYPGYFYHUYAUVNUXSGUVIYPUVKHVSXGYIUVKYRUMZUXRYTUXTUUBUYBUVLYSYQIUVKYRGYFYJ
      UYBUXSUUAGUVKYRYPHYKXGYIYLWGYM $.
      $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem3 $p |- ( S e. ( SubGrpOp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vy csubgo cfv wcel cgiso csymg cgr crn cvv rnexg ax-mp eqeltri symggrpi
      co cayleylem2 ghomgrpi cghom wf1o wa wb wss w3a issubgo mpbi simp2i mp2an
      elgiso ghomgsg mp3an cv cgi wceq wi wral cxp cres wfo wfn eqid ghomfo syl
      forn eqtri grpofo fofn fnresdm 3syl eqcomi ghomf1o subgoid fveq2i symgidi
      cid eqtr2i eqeq2i fveq1 grpoidcl fvresi syl6eq grplactval mp3an13 grporid
      sylbir eqtrd eqeq1d syl5ib mprgbir f1oeq3 f1oeq2 bitri mpbir2an pm3.2i
      mpan ) BIUCUDUEZGHBUFUOUEZBGHIKNIJUGUDZUHRJJHUIZUJPHUHUEZXRUJUENHUHUKULUM
      ZUNUMZABCDEFGHIJKLMNOPQRSTUAUPZTUAUQZXPGHBURUOUEZXRBUIZGUSZXSBUHUEZXPYDYF
      UTVANIUHUEZYGBIVBZXOYHYGYIVCYCIBVDVEVFZGHBVHVGXSYHGHIURUOUEZYDNYAYBBGHIKT
      UAVIVJZJKGUSZYFYMUBVKZGUDZBVLUDZVMZYNCVMZVNZUBJXSYGYDYMYSUBJVOVANYJYLUBBY
      PCGHBJKKPTBKKVPZVQZBYGUUABVMZYJYGYTKBVRBYTVSUUBBKKGUIZYETXSYHYKUUCYEVMZNY
      AYBXSYHYKVCJYEGVRUUDBGHIJKYEPTUAYEVTWAJYEGWCWBVJWDZWEYTKBWFYTBWGWHULWIUUE
      QYPVTZWJVJYQCYOUDZCVMZYNJUEZYRYQYOWNJVQZVMZUUHUUJYPYOYPIVLUDZUUJXOYPUULVM
      YCYPUULIBUULVTUUFWKULUULXQVLUDUUJIXQVLRWLJXTWMWDWOWPUUKUUGCUUJUDZCCYOUUJW
      QCJUEZUUMCVMXSUUNNCHJPQWRULZJCWSULWTXDUUIUUGYNCUUIUUGYNCHUOZYNXSUUIUUNUUG
      UUPVMNUUOYNCEFGHJLMSPXAXBXSUUIUUPYNVMNYNCHJPQXCXNXEXFXGXHYMJYEGUSZYFKYEVM
      YMUUQVAUUEKYEJGXIULJXRVMUUQYFVAPJXRYEGXJULXKVEXLXM $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F f $.  $d G a b f g h $.  $d X a b f g h $.
    cayleyi.1 $e |- G e. GrpOp $.
    cayleyi.2 $e |- X = ran G $.
    cayleyi.3 $e |- H = ( SymGrp ` X ) $.
    cayleyi.4 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                          ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleyi.5 $e |- Y = ran F $.
    cayleyi.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is an
       isomorphism between ` G ` and the subgroup ` S ` of the symmetry group
       ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by Paul
       Chapman, 3-Mar-2008.) $)
    cayleyi $p |- ( S e. ( SubGrpOp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vf cv wf1o eqid cab cgi cfv cayleylem3 ) GGQRSQUAZAEUBUCZQBCDEFGHIJKUET
      LUFTMNOPUD $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d F f h $.  $d G a b g j $.  $d G f h $.  $d H f h $.  $d Q a b g j $.
    $d Q f h $.  $d S h $.  $d X a b g j $.
    cayleythlem.1 $e |- Q = { <. <. x , x >. , x >. } $.
    cayleythlem.2 $e |- X = ran if ( G e. GrpOp , G , Q ) $.
    cayleythlem.3 $e |- H = ( SymGrp ` X ) $.
    cayleythlem.4 $e |- F = { <. g , j >. | ( g e. X /\ j = { <. a , b >. |
      ( a e. X /\ b = ( g if ( G e. GrpOp , G , Q ) a ) ) } ) } $.
    cayleythlem.5 $e |- Y = ran F $.
    cayleythlem.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Lemma for ~ cayleyth . $)
    cayleythlem $p |- ( G e. GrpOp ->
        E. f E. h e. ( SubGrpOp ` ( SymGrp ` ran G ) ) f e. ( G GrpIso h ) ) $=
      ( cgr wcel cv cgiso csubgo cfv wrex wex crn csymg cif wceq eleq2d rexbidv
      co oveq1 exbidv wa cop csn vex grposn eqeltri cayleyi oveq2 rcla4ev ax-mp
      elimel copab rnexg fopabex2 eleq1 cla4ev dedth iftrue rneqd syl5eq fveq2d
      cvv rexeqdv mpbid ) IUAUBZDUCZIFUCZUDUOZUBZFJUEUFZUGZDUHZWFFIUIZUJUFZUEUF
      ZUGZDUHWBWIWCWBIBUKZWDUDUOZUBZFWGUGZDUHZIBIWNULZWHWQDWSWFWPFWGWSWEWOWCIWN
      WDUDUPUMUNUQHWOUBZFWGUGZWRCWGUBHWNCUDUOZUBZURXACEGHWNJKLMNIBUABAUCZXDUSXD
      USUTUAOXDAVAVBVCVHZPQRSTVDWTXCFCWGWDCULWOXBHWDCWNUDVEUMVFVGWQXADHEGKMUCZK
      UBNUCEUCXFWNUOULURMNVIHKWNUIZVSPWNUAUBXGVSUBXEWNUAVJVGVCRVKWCHULWPWTFWGWC
      HWOVLUNVMVGVNWBWHWMDWBWFFWGWLWBJWKUEWBJKUJUFWKQWBKWJUJWBKXGWJPWBWNIWBIBVO
      VPVQVRVQVRVTUQWA $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d G a b f g h j $.  $d a b f g h j x $.
    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic to
       a subgroup of the symmetry group on the underlying set of ` G ` .  (For
       any group ` G ` there exists an isomorphism ` f ` between ` G ` and a
       subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. GrpOp
          -> E. f E. h e. ( SubGrpOp ` ( SymGrp ` ran G ) )
                                      f e. ( G GrpIso h ) ) $=
      ( vx vg vj va vb cv cop csn cgr wcel cif crn csymg wceq wa copab eqid cfv
      co cxp cres cayleythlem ) DDIZUFJUFJKZCLMCUGNZOZPUAZEIZUIMFIGIZUIMHIUKULU
      HUBQRGHSQREFSZOZUNUCUDZAEBFUMCUJUIUNGHUGTUITUJTUMTUNTUOTUE $.
      $( [3-Mar-2008] $)
  $}

