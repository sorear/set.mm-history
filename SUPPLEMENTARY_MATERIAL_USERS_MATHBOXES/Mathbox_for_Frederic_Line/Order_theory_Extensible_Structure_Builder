$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Order theory (Extensible Structure Builder)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c OrHom $.

  $( Extend class notation with the the class of all decreasing functions. $)
  corhom $a class OrHom $.

  $c OrIso $.

  $( Extend class notation with the the class of all the order isomorphisms. $)
  coriso $a class OrIso $.

  $c Preset $.

  $( Extend class notation with the class of all the presets. $)
  cpreset $a class Preset $.

  $c Toset $.

  $( Extend class notation with the class of all tosets. $)
  ctos $a class Toset $.

  ${
    $d f b r x y z $.
    $( Define the class of preordered sets (presets).  A preset is a transitive
       and reflexive relation. $)
    df-preset $a |- Preset = { f | [ ( Base ` f ) / b ] [ ( le ` f ) / r ]
                A. x e. b A. y e. b A. z e. b ( x r x
                 /\ ( ( x r y /\ y r z ) -> x r z ) ) } $.
  $}

  ${
    $d f b r x y $.
    $( Define the class of totally ordered sets (tosets). $)
    df-toset $a |- Toset = { f e. Poset | [ ( Base ` f ) / b ]
      [ ( le ` f ) / r ] A. x e. b A. y e. b ( x r y \/ y r x ) } $.
  $}

  ${
    $d b f r x y B $.  $d b f r K $.  $d b f r x y L $.
    istos.b $e |- B = ( Base ` K ) $.
    istos.l $e |- L = ( le ` K ) $.
    $( The predicate "is a toset."  (Contributed by FL, 17-Nov-2014.) $)
    istos $p |- ( K e. Toset <-> ( K e. Poset
               /\ A. x e. B A. y e. B ( x L y \/ y L x ) ) ) $=
      ( vr vb vf cv wbr wral cple cfv wsbc cbs wceq wb wi wo cpo ctos wcel fvex
      cvv fveq2 dfsbcq sbcbidgv mpan2 bitrd wa eqtr breq orbi12d 2ralbidv raleq
      raleqbi1dv sylan9bb ex expcom eqcoms ax-mp syl5com sbc2ie syl6bb df-toset
      syl imp elrab2 ) AKZBKZHKZLZVLVKVMLZUAZBIKZMAVQMZHJKZNOZPZIVSQOZPZVKVLELZ
      VLVKELZUAZBCMZACMZJDUBUCVSDRZWCVRHDNOZPZIDQOZPZWHWIWCWKIWBPZWMWIWBUFUDWCW
      NSVSQUEWIWAWKIWBUFWIVTWJRWAWKSVSDNUGVRHVTWJUHVHUIUJWIWBWLRWNWMSVSDQUGWKIW
      BWLUHVHUKVRWHIHWLWJDQUEDNUEVQWLRZVMWJRZVRWHSZCWLRWOWPWQTZTZFWSWLCWOWLCRZW
      RWOWTULVQCRZWPWQVQWLCUMEWJRWPXAWQTZTZGXCWJEWPWJERZXBWPXDULVMERZXBVMWJEUMX
      EXAWQXEVRWFBVQMZAVQMXAWHXEVPWFABVQVQXEVNWDVOWEVKVLVMEUNVLVKVMEUNUOUPXFWGA
      VQCWFBVQCUQURUSUTVHVAVBVCVDVAVBVCVIVEVFABJHIVGVJ $.
      $( [17-Nov-2014] $)
  $}

  ${
    $d r s f a b $.
    $( Increasing functions also called "order homomorphisms", "isotone,
       monotone or order preserving mappings".  To have the class of decreasing
       functions use ` ( r OrHom ``' s ) ` .  Experimental.  Bourbaki
       E.III.7 $)
    df-orhom $a |- OrHom = ( r e. _V , s e. _V
       |-> { f e. ( ( Base ` s ) ^m ( Base ` r ) ) |
         A. a e. ( Base ` r ) A. b e. ( Base ` r ) ( a ( le ` r ) b ->
           ( f ` a ) ( le ` s ) ( f ` b ) ) } ) $.

    $( Order isomorphisms.  Experimental.  Bourbaki E.III.5 $)
    df-oriso $a |- OrIso = ( r e. _V , s e. _V
       |-> { f | ( f : ( Base ` r ) -1-1-onto-> ( Base ` s ) /\
         A. a e. ( Base ` r ) A. b e. ( Base ` r ) ( a ( le ` r ) b <->
           ( f ` a ) ( le ` s ) ( f ` b ) ) ) } ) $.
  $}

  ${
    $d r s <_a $.  $d a b f r s A $.  $d r s <_b $.  $d a b f r s B $.
    $d f r s X $.  $d f r s Y $.
    isorhom.1 $e |- X = ( Base ` A ) $.
    isorhom.2 $e |- Y = ( Base ` B ) $.
    isorhom.3 $e |- <_a = ( le ` A ) $.
    isorhom.4 $e |- <_b = ( le ` B ) $.
    $( The predicate is an order homomorphism.  (Contributed by FL,
       17-Nov-2014.) $)
    isorhom $p |- ( ( A e. C /\ B e. D ) ->
      ( A OrHom B ) = { f e. ( Y ^m X ) |
         A. a e. X A. b e. X ( a <_a b ->
           ( f ` a ) <_b ( f ` b ) ) } ) $=
      ( wcel cfv wral cmap cple vr vs cvv corhom co cv wbr crab wceq elex fveq2
      wi cbs oveq2d eleq2d imbi1d imbi12d ralbidv2 rabeqbidv syl6eqr eqcomi a1i
      breqd oveq12d df-orhom ovex rabex ovmpt2 syl2an ) ACPAUCPBUCPABUDUEHUFZIU
      FZJUGZVJEUFZQZVKVMQZKUGZULZIFRZHFRZEGFSUEZUHZUIBDPACUJBDUJUAUBABUCUCVJVKU
      AUFZTQZUGZVNVOUBUFZTQZUGZULZIWBUMQZRZHWIRZEWEUMQZWISUEZUHWAUDVJVKATQZUGZW
      GULZIAUMQZRZHWQRZEWLWQSUEZUHWBAUIZWKWSEWMWTXAWIWQWLSWBAUMUKZUNXAWJWRHWIWQ
      XAVJWIPVJWQPZWJWRXAWIWQVJXBUOXAWHWPIWIWQXAVKWIPVKWQPZWHWPXAWIWQVKXBUOXAWD
      WOWGXAWCWNVJVKWBATUKVCUPUQURUQURUSWEBUIZWSVSEWTVTXEWLGWQFSXEWLBUMQGWEBUMU
      KMUTWQFUIXEFWQLVAVBZVDXEWRVRHWQFXEXCVJFPWRVRXEWQFVJXFUOXEWPVQIWQFXEXDVKFP
      WPVQXEWQFVKXFUOXEWOVLWGVPXEWNJVJVKWNJUIXEJWNNVAVBVCXEWFKVNVOXEWFBTQKWEBTU
      KOUTVCUQUQURUQURUSEUBUAHIVEVSEVTGFSVFVGVHVI $.
      $( [17-Nov-2014] $)
  $}

  ${
    isoriso.1 $e |- X = ( Base ` A ) $.
    isoriso.2 $e |- Y = ( Base ` B ) $.
    isoriso.3 $e |- <_a = ( le ` A ) $.
    isoriso.4 $e |- <_b = ( le ` B ) $.
    ${
      $d r s <_a $.  $d a b f r s A $.  $d r s <_b $.  $d a b f r s B $.
      $d f r s X $.  $d f r s Y $.
      $( Order isomorphisms.  (Contributed by FL, 17-Nov-2014.) $)
      isoriso $p |- ( ( A e. C /\ B e. D ) -> ( A OrIso B ) =
         { f | ( f : X -1-1-onto-> Y /\ A. a e. X A. b e. X
           ( a <_a b <-> ( f ` a ) <_b ( f ` b ) ) ) } ) $=
        ( wcel cfv wral wceq cbs vr vs cvv coriso co cv wf1o wbr wb wa cab elex
        cple fveq2 eqidd f1oeq23 syl2anc bibi1d raleqbidv anbi12d abbidv bibi2d
        breqd 2ralbidv eqcomi mp2an breqi bibi12i imbi12i ralbii2 anbi12i abbii
        eleq2i syl6eq df-oriso fvex eqeltri adantr pm4.71ri fabex ovmpt2 syl2an
        wf f1of ) ACPAUCPBUCPABUDUEFGEUFZUGZHUFZIUFZJUHZWGWEQZWHWEQZKUHZUIZIFRZ
        HFRZUJZEUKZSBDPACULBDULUAUBABUCUCUAUFZTQZUBUFZTQZWEUGZWGWHWRUMQZUHZWJWK
        WTUMQZUHZUIZIWSRZHWSRZUJZEUKWQUDATQZXAWEUGZWGWHAUMQZUHZXFUIZIXKRZHXKRZU
        JZEUKZWRASZXJXREXTXBXLXIXQXTWSXKSXAXASXBXLUIWRATUNZXTXAUOWSXKXAXAWEUPUQ
        XTXHXPHWSXKYAXTXGXOIWSXKYAXTXDXNXFXTXCXMWGWHWRAUMUNVCURUSUSUTVAWTBSZXSX
        KBTQZWEUGZXNWJWKBUMQZUHZUIZIXKRZHXKRZUJZEUKWQYBXRYJEYBXLYDXQYIYBXKXKSXA
        YCSXLYDUIYBXKUOWTBTUNXKXKXAYCWEUPUQYBXOYGHIXKXKYBXFYFXNYBXEYEWJWKWTBUMU
        NVCVBVDUTVAYJWPEYDWFYIWOXKFSYCGSYDWFUIFXKLVEZGYCMVEXKFYCGWEUPVFYHWNHXKF
        WGXKPWGFPYHWNXKFWGYKVMYGWMIXKFWHXKPWHFPYGWMXKFWHYKVMXNWIYFWLWGWHXMJJXMN
        VEVGWJWKYEKKYEOVEVGVHVIVJVIVJVKVLVNEUBUAHIVOWPEFGWQFXKUCLATVPVQGYCUCMBT
        VPVQWPFGWEWCZWPUJEWPYLWFYLWOFGWEWDVRVSVLVTWAWB $.
        $( [17-Nov-2014] $)
    $}

    ${
      $d a b f A $.  $d a b f B $.  $d a b f F $.  $d b f X $.  $d f <_a $.
      $d f <_b $.  $d f Y $.
      $( Order isomorphisms.  (Contributed by FL, 17-Nov-2014.) $)
      isoriso2 $p |- ( ( A e. C /\ B e. D /\ F e. E ) ->
        ( F e. ( A OrIso B ) <-> ( F : X -1-1-onto-> Y /\
         A. a e. X A. b e. X ( a <_a b <->
           ( F ` a ) <_b ( F ` b ) ) ) ) ) $=
        ( vf wcel cfv wral w3a coriso co cv wf1o wbr wb wa wceq isoriso 3adant3
        cab eleq2d f1oeq1 fveq1 breq12d bibi2d 2ralbidv anbi12d elabg 3ad2ant3
        bitrd ) ACRZBDRZFERZUAZFABUBUCZRFGHQUDZUEZIUDZJUDZKUFZVJVHSZVKVHSZLUFZU
        GZJGTIGTZUHZQULZRZGHFUEZVLVJFSZVKFSZLUFZUGZJGTIGTZUHZVFVGVSFVCVDVGVSUIV
        EABCDQGHIJKLMNOPUJUKUMVEVCVTWGUGVDVRWGQFEVHFUIZVIWAVQWFGHVHFUNWHVPWEIJG
        GWHVOWDVLWHVMWBVNWCLVJVHFUOVKVHFUOUPUQURUSUTVAVB $.
        $( [17-Nov-2014] $)
    $}

    ${
      $d a b x y <_a $.  $d a b x y <_b $.  $d a b x y A $.  $d a b x y B $.
      $d a b C $.  $d a b D $.  $d a b x y F $.  $d a b x y X $.  $d a b Y $.
      $( If ` F ` is an order isomorphism so is ` ``' F ` .  (Contributed by
         FL, 11-Nov-2014.) $)
      oriso $p |- ( ( A e. C /\ B e. D ) ->
        ( F e. ( A OrIso B ) -> `' F e. ( B OrIso A ) ) ) $=
        ( wcel wa wi wbr cfv wb cvv vx vy va vb coriso co ccnv w3a wf1o cv wral
        f1ocnv ad2antrl wf f1of ffvelrn ex syl im2anan9 wceq breq1 fveq2 breq1d
        bibi12d breq2 breq2d rcla42v bicom simp3 simp2l f1ocnvfv2 simp2r bibi1d
        syl2anc breq12d biimpd 3exp com14 sylbi syl6 com25 pm2.43i syli a1d imp
        com24 adantl ralrimivv jca isoriso2 elex cnvexg 3anim123i 3com12 3expia
        3imtr4d pm2.43d ) ACNZBDNZOEABUEUFZNZEUGZBAUEUFNZWRWSXAXAXCPWRWSXAUHZFG
        EUIZUAUJZUBUJZHQZXFERZXGERZIQZSZUBFUKUAFUKZOZGFXBUIZUCUJZUDUJZIQZXPXBRZ
        XQXBRZHQZSZUDGUKUCGUKZOZXAXCXDXNYDXDXNOZXOYCXEXOXDXMFGEULZUMYEYBUCUDGGX
        NXPGNZXQGNZOZYBPZXDXNYJXEXMXNYJPZXEXMYKPZXEXOXOXEYLPYFYFXOXOOZXNXMXEYJY
        MXMXEYJPPXNYMYIXEXMYBYIYMXSFNZXTFNZOZXEXMYBPPZXOYGYNXOYHYOXOGFXBUNZYGYN
        PGFXBUOZYRYGYNGFXPXBUPUQURXOYRYHYOPYSYRYHYOGFXQXBUPUQURUSYPYIYQPYPXMYIX
        EYPYBYPXMYAXSERZXTERZIQZSZYIXEYPYBPPPZXLUUCXSXGHQZYTXJIQZSUAUBXSXTFFXFX
        SUTZXHUUEXKUUFXFXSXGHVAUUGXIYTXJIXFXSEVBVCVDXGXTUTZUUEYAUUFUUBXGXTXSHVE
        UUHXJUUAYTIXGXTEVBVFVDVGUUCUUBYASZUUDYAUUBVHYPYIXEUUIYBYPYIXEUUIYBPYPYI
        XEUHZUUIYBUUJUUBXRYAUUJYTXPUUAXQIUUJXEYGYTXPUTYPYIXEVIZYPYGYHXEVJFGXPEV
        KVNUUJXEYHUUAXQUTUUKYPYGYHXEVLFGXQEVKVNVOVMVPVQVRVSVTWAWBWCWFWDWFVNWBWE
        WBWGWHWIUQABCDWTEFGUAUBHIJKLMWJXDBTNZATNZXBTNZUHZXCYDSWSWRXAUUOWSUULWRU
        UMXAUUNBDWKACWKEWTWLWMWNBATTTXBGFUCUDIHKJMLWJURWPWOWQ $.
        $( [11-Nov-2014] $)
    $}

  $}


