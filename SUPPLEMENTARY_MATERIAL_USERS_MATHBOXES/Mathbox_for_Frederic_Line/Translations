$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Translations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    trfun.2 $e |- F = ( x e. X |-> ( x G A ) ) $.
    trinv.1 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a right translation.  The term ` A ` is a constant:
         ` x ` is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trdom2 $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq ovex a1i ralrimiva cmpdom sylib )
        DHIBEIJZAKZBDLZMIZAENCOEPUBUEAEUEUBUCEIJUCBDQRSAEUDCFTUA $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d X x $.
      $( A right translation is a set.  (Contributed by FL, 19-Sep-2010.) $)
      trset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEAMZBDTCFNUAUBELAEUCBCDFODEGPQLCRS $.
        $( [19-Sep-2010] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a right translation.  The term ` A ` is a constant: ` x `
         is not present.  (Contributed by FL, 21-Jun-2010.) $)
      trran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa cv co wceq wi grpocl cfv eqid adantr eqcomd wrex rnmpt
        crn cab 3exp com23 impcom eleq1 syl5ibrcom expcom rexlimdv simpll simpr
        imp cgn grpoinvcl syl3anc cgi grpolinv grporid adantlr grpoass syl13anc
        oveq2d simplr 3eqtr4d eqeq2d rcla4ev syl2anc ex impbid abbi1dv syl5eq
        oveq1 ) DIJZBEJZKZCUCHLZALZBDMZNZAEUAZHUDEAHEVTCFUBVQWBHEVQWBVREJZVQWAW
        CAEVSEJZVQWAWCOWDVQKWCWAVTEJZVQWDWEVOVPWDWEOVOWDVPWEVOWDVPWEVSBDEGPUEUF
        UNUGVRVTEUHUIUJUKVQWCWBVQWCKZVRBDUOQZQZDMZEJZVRWIBDMZNZWBWFVOWCWHEJZWJV
        OVPWCULZVQWCUMZVQWMWCBDWGEGWGRZUPSZVRWHDEGPUQWFVRDURQZDMZVRWHBDMZDMZVRW
        KWFWRWTVRDWFWTWRVQWTWRNWCBWRDWGEGWRRZWPUSSTVDVOWCVRWSNVPVOWCKWSVRVRWRDE
        GXBUTTVAWFVOWCWMVPWKXANWNWOWQVOVPWCVEVRWHBDEGVBVCVFWAWLAWIEVSWINVTWKVRV
        SWIBDVNVGVHVIVJVKVLVM $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d G x y z $.  $d X x y z $.
      $( A right translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( vy vz wcel wa wfn crn wceq cv cfv wi wral cvv wb cgr wf1o ovex mptfng
        co rgenw a1i mpbii trran2 fvmpt2 mpan2 ad2antrl simpr adantl fvmptg syl
        jctir oveq1 eqeq12d biimpd grporcan 3exp2 imp3a com23 sylibd ralrimivva
        imp31 cmpt hbmpt1 ax-17 dff1o6f f1oeq1 ax-mp fneq1i rneqi eqeq1i fveq1i
        w3a eqeq12i imbi1i 2ralbii 3anbi123i 3bitr4i syl3anbrc ) DUAJZBEJZKZCEL
        ZCMZENZAOZCPZHOZCPZNZWKWMNZQZHERAERZEECUBZWGWKBDUEZSJZAERZWHXAAEWKBDUCZ
        UFXBWHTWGAEWTCFUDUGUHABCDEFGUIWGWQAHEEWGWKEJZWMEJZKZKZWOWTWMBDUEZNZWPXG
        WOXIXGWLWTWNXHXDWLWTNZWGXEXDXAXJXCAEWTSCFUJUKULXGXEXHSJZKZWNXHNXFXLWGXF
        XEXKXDXEUMWMBDUCUQUNAWMWTXHESCWKWMBDURFUOUPUSUTWEWFXFXIWPTZWEXFWFXMWEXD
        XEWFXMQWEXDXEWFXMWKWMBDEGVAVBVCVDVGVEVFEEAEWTVHZUBZXNELZXNMZENZWKXNPZWM
        XNPZNZWPQZHERAERZVRWSWHWJWRVRAHIEEXNAIEWTVIIOXNJHVJVKCXNNWSXOTFEECXNVLV
        MWHXPWJXRWRYCECXNFVNWIXQECXNFVOVPWQYBAHEEWOYAWPWLXSWNXTWKCXNFVQWMCXNFVQ
        VSVTWAWBWCWD $.
        $( [31-Dec-2010] $) $( [21-Jun-2010] $)
    $}

    ${
      $d A x y z $.  $d D y z $.  $d G x y z $.  $d X x y z $.
      trinv.2 $e |- D = ( /g ` G ) $.
      $( The converse of a right translation.  The term ` A ` is a constant.
         (Contributed by FL, 21-Jun-2010.) $)
      trinv $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( x D A ) ) ) $=
        ( vy vz wcel wa co cmpt wceq cfv wral cvv wfn ccnv cv wf1o trooo adantr
        cgr grpodivcl 3exp com23 imp31 jca oveq1 cbvmptv eqtri fvmptg grponpcan
        ovex sylancl eqtrd f1ocnvfv sylc eqid fvmpt2 eqtr4d ralrimiva wb dff1o4
        simpr simprbi syl rgenw mptfng a1i hbmpt1 eqfnfv2f cnveqi fneq1i anbi1i
        mpbii hbcnv eqeq1i fveq1i ralbii bibi12i 3imtr4i syl2anc mpbird ) EUFLZ
        BFLZMZDUAZAFAUBZBCNZOZPZWLWKQZWLWNQZPZAFRZWJWRAFWJWLFLZMZWPWMWQXAFFDUCZ
        WMFLZMWMDQZWLPWPWMPXAXBXCWJXBWTABDEFGHUDZUEWHWIWTXCWHWTWIXCWHWTWIXCWLBC
        EFHIUGUHUIUJZUKXAXDWMBENZWLXAXCXGSLXDXGPXFWMBEUQJWMJUBZBENZXGFSDXHWMBEU
        LDAFWLBENZOZJFXIOGAJFXJXIWLXHBEULUMUNUOURWHWIWTXGWLPZWHWTWIXLWHWTWIXLWL
        BCEFHIUPUHUIUJUSFFWMWLDUTVAXAWTWMSLZWQWMPWJWTVHWLBCUQZAFWMSWNWNVBZVCURV
        DVEWJWKFTZWNFTZWOWSVFZWJXBXPXEXBDFTXPFFDVGVIVJWJXMAFRZXQXMAFXNVKXSXQVFW
        JAFWMWNXOVLVMVSXKUAZFTZXQMXTWNPZWLXTQZWQPZAFRZVFXPXQMXRAKFXTWNAKXKAKFXJ
        VNVTAKFWMVNVOXPYAXQFWKXTDXKGVPZVQVRWOYBWSYEWKXTWNYFWAWRYDAFWPYCWQWLWKXT
        YFWBWAWCWDWEWFWG $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d A a b x y $.  $d B a b x y $.  $d F x $.  $d G a b x y $.
      $d X a b x y $.
      cmprtr.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmprtr2 .  (Contributed by FL,
         17-Oct-2010.) $)
      cmprtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy va vb wcel cv co wceq wa copab cmpt cgr w3a ccom crn trran2 eqimss
        wss syl 3adant2 ovex oveq1 df-mpt eqtri wb eleq1 adantr eqeq12d anbi12d
        weq simpr cbvopabv 3eqtri fopabco simpl1 simpl3 simpl2 grpoass syl13anc
        eqid eqeq2d pm5.32da opabbidv eqcomi a1i 3eqtrd ) EUANZBGNZCGNZUBZDFUCZ
        AOZGNZKOZWACEPZBEPZQZRZAKSZWBWCWACBEPEPZQZRZAKSZAGWITZVSFUDZGUGZVTWHQVP
        VRWOVQVPVRRWNGQWOACFEGJIUEWNGUFUHUIAKLMKGGWDLOZBEPZWEFDWHWACEUJWPBEUJWD
        BEUJWPWDBEUKFAGWDTWBWCWDQRAKSJAKGWDULUMDAGWABEPZTWBWCWRQZRZAKSWPGNZMOZW
        QQZRZLMSHAKGWRULWTXDAKLMALUSZKMUSZRZWBXAWSXCXEWBXAUNXFWAWPGUOUPXGWCXBWR
        WQXEXFUTXEWRWQQXFWAWPBEUKUPUQURVAVBWHVIVCUHVSWGWKAKVSWBWFWJVSWBRZWEWIWC
        XHVPWBVRVQWEWIQVPVQVRWBVDVSWBUTVPVQVRWBVEVPVQVRWBVFWACBEGIVGVHVJVKVLWLW
        MQVSWMWLAKGWIULVMVNVO $.
        $( [17-Oct-2010] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d F y $.  $d G x y $.  $d X x y $.
      cmprtr2.1 $e |- H = ( x e. X |-> ( x G B ) ) $.
      $( Composite of two right translations.  ( ~ cmprtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      cmprtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( x G ( B G A ) ) ) ) $=
        ( vy cgr wcel w3a cv co cmpt oveq1 cbvmptv eqtri ccom cmprtr syl6eq ) E
        LMBGMCGMNDFUAKGKOZCBEPZEPZQAGAOZUEEPZQKBCDEFGDAGUGBEPZQKGUDBEPZQHAKGUIU
        JUGUDBERSTIFAGUGCEPZQKGUDCEPZQJAKGUKULUGUDCERSTUBKAGUFUHUDUGUEERSUC $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d B a u $.  $d F a u $.  $d G x $.  $d X x $.
      $( The image of a set through a translation.  (Contributed by FL,
         30-Dec-2010.) $)
      imtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X ) ->
       ( F " B ) = { a | E. u e. B a = ( F ` u ) } ) $=
        ( cgr wcel cpw w3a cima cv wceq wrex cab wi cfv cdm co cmpfun wa trdom2
        wfun wss pweq eleq2d biimpd eqcoms elpwi syl6 syl dfimafn sylancr eqcom
        3impia rexbii abbii syl6eq ) FKLZCGLZDGMZLZNZEDOZBPEUAZHPZQZBDRZHSZVJVI
        QZBDRZHSVGEUGDEUBZUHZVHVMQAGAPCFUCEIUDVCVDVFVQVCVDUEVPGQZVFVQTACEFGIJUF
        VRVFDVPMZLZVQVFVTTGVPGVPQZVFVTWAVEVSDGVPUIUJUKULDVPUMUNUOUSBHDEUPUQVLVO
        HVKVNBDVIVJURUTVAVB $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a v x $.  $d B a x $.  $d F a x $.  $d G a v x $.  $d X a x $.
      prsubrtr.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  (Contributed by FL,
         30-Dec-2010.) $)
      prsubrtr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va vv cgr wcel cpw cv co wceq wrex wb w3a csn cab cfv cima elsn oveq2
        wa eqeq2d sylbi rexbiia c0 snnzg 3ad2ant2 adantr r19.9rzv bicomd syl5bb
        wne syl cvv wi elelpwi expcom 3ad2ant3 ovex fvmpt2 sylancl eqcomd bitrd
        imp rexbidva abbidv cdm simp1 crn grporndm syl5eq eleq2d biimpa 3adant2
        pweqd snelpwg adantl mpbid 3adant3 eqid iscst2 syl3anc imtr 3eqtr4d ) E
        MNZBGNZCGOZNZUAZKPZAPZLPZEQZRZLBUBZSZACSZKUCZWQWRDUDZRZACSZKUCCXBFQZDCU
        EWPXDXHKWPXCXGACWPWRCNZUHZXCWQWRBEQZRZXGXCXMLXBSZXKXMXAXMLXBWSXBNWSBRZX
        AXMTLBUFXOWTXLWQWSBWREUGUIUJUKXKXBULUSZXNXMTWPXPXJWMWLXPWOBGUMUNUOXPXMX
        NXMLXBUPUQUTURXKXLXFWQXKXFXLXKWRGNZXLVANXFXLRWPXJXQWOWLXJXQVBWMXJWOXQWR
        CGVCVDVEVKWRBEVFAGXLVADHVGVHVIUIVJVLVMWPWLCEVNVNZOZNZXBXSNZXIXERWLWMWOV
        OWLWOXTWMWLWOXTWLWNXSCWLGXRWLGEVPXRIEVQVRZWBVSVTWAWLWMYAWOWLWMUHBXRNZYA
        WLWMYCWLGXRBYBVSVTWMYCYATWLBXRGWCWDWEWFLACXBEFMXRKXRWGJWHWIAABCDEGKHIWJ
        WK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d A a x $.  $d B a $.  $d F a $.  $d G a x $.  $d X a x $.
      prsubrtr2.1 $e |- H = ( cset ` G ) $.
      $( The product of a subset ` B ` of ` X ` by an element of ` X ` is the
         image of ` B ` by a right translation.  ( ~ prsubrtr with a distinct
         variable condition relaxed.)  (Contributed by FL, 1-Jan-2011.) $)
      prsubrtr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. ~P X )
        -> ( B H { A } ) = ( F " B ) ) $=
        ( va cv co cmpt oveq1 cbvmptv eqtri prsubrtr ) KBCDEFGDAGALZBEMZNKGKLZB
        EMZNHAKGTUBSUABEOPQIJR $.
        $( [1-Jan-2011] $)
    $}

    ${
      $d A x $.  $d G x $.  $d X x $.
      caytr.1 $e |- H = ( cset ` G ) $.
      $( "It follows that if the entire group is multiplied by any one of the
         symbols, either as further or nearer factor, the effect is simply to
         reproduce the group... ."  Cayley, On the theory of groups, as
         depending on the symbolic equation th^n = 1, 1854.  (it is the
         original paper where the axiomatic definition of a group was given for
         the first time.)  (Contributed by FL, 15-Oct-2012.) $)
      caytr $p |- ( ( G e. GrpOp /\ A e. X ) -> ( X H { A } ) = X ) $=
        ( cgr wcel wa csn co cima crn cpw wceq wss cvv ssid unsgrp adantr elpwg
        wb syl mpbiri prsubrtr2 mpd3an3 wf1o wfn trooo f1ofn 3syl trran2 3eqtrd
        fnima ) DJKZBFKZLZFBMENZCFOZCPZFURUSFFQKZVAVBRUTVDFFSZFUAUTFTKZVDVEUEUR
        VFUSDFHUBUCFFTUDUFUGABFCDEFGHIUHUIUTFFCUJCFUKVBVCRABCDFGHULFFCUMFCUQUNA
        BCDFGHUOUP $.
        $( [15-Oct-2012] $)
    $}

  $}

  ${
    ltrdom.1 $e |- F = ( x e. X |-> ( A G x ) ) $.
    ltrdom.2 $e |- X = ran G $.
    ${
      $d A x $.  $d G x $.  $d X x $.
      $( The domain of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 26-Apr-2012.) $)
      ltrdom $p |- ( ( G e. GrpOp /\ A e. X ) -> dom F = X ) $=
        ( cgr wcel wa cv co cvv wral cdm wceq ovex a1i ralrimiva cmpdom sylib )
        DHIBEIJZBAKZDLZMIZAENCOEPUBUEAEUEUBUCEIJBUCDQRSAEUDCFTUA $.
        $( [26-Apr-2012] $)
    $}

    ${
      $d X x $.
      $( A left translation is a set.  (Contributed by FL, 28-Apr-2012.) $)
      ltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv cmpfun cmpdom2 unsgrp syl5eqel funex sylancr
        co ) DHIZCJCKZLICLIAEBAMZDTCFNUAUBELAEBUCCDFODEGPQLCRS $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( The range of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 28-Apr-2012.) $)
      ltrran2 $p |- ( ( G e. GrpOp /\ A e. X ) -> ran F = X ) $=
        ( vy cgr wcel wa crn cv co wceq grpocl cfv eqid adantr eqcomd cab rnmpt
        wrex wi 3expia impcom eleq1 syl5ibrcom expcom rexlimdv simpll grpoinvcl
        cgn syl3anc cgi grpolid adantlr grporinv oveq1d simplr grpoass syl13anc
        simpr 3eqtrd oveq2 eqeq2d rcla4ev syl2anc ex impbid abbi1dv syl5eq ) DI
        JZBEJZKZCLHMZBAMZDNZOZAEUCZHUAEAHEVRCFUBVOVTHEVOVTVPEJZVOVSWAAEVQEJZVOV
        SWAUDWBVOKWAVSVREJZVOWBWCVMVNWBWCBVQDEGPUEUFVPVREUGUHUIUJVOWAVTVOWAKZBD
        UMQZQZVPDNZEJZVPBWGDNZOZVTWDVMWFEJZWAWHVMVNWAUKZVOWKWABDWEEGWERZULSZVOW
        AVCZWFVPDEGPUNWDVPDUOQZVPDNZBWFDNZVPDNZWIVMWAVPWQOVNVMWAKWQVPVPWPDEGWPR
        ZUPTUQWDWPWRVPDVOWPWROWAVOWRWPBWPDWEEGWTWMURTSUSWDVMVNWKWAWSWIOWLVMVNWA
        UTWNWOBWFVPDEGVAVBVDVSWJAWGEVQWGOVRWIVPVQWGBDVEVFVGVHVIVJVKVL $.
        $( [28-Apr-2012] $)
    $}

    ${
      $d A a b x z $.  $d G a b x z $.  $d X a b x z $.
      $( A left translation is a bijection.  The term ` A ` is a constant.
         (Contributed by FL, 29-Apr-2012.) $)
      ltrooo $p |- ( ( G e. GrpOp /\ A e. X ) -> F : X -1-1-onto-> X ) $=
        ( va vb cv co wceq wcel wa wf1o wi cfv wral 3exp 3adant3 cmpt cgr rgenw
        wfn crn weq cvv ovex a1i eqid mptfng sylib ltrran2 simprl grpocl adantr
        w3a com3r com3l imp31 oveq2 fvmptg syl2anc eqcomd simprr 3expia adantld
        simp3 imp jca 3eqtrd wb grpolcan expcom com34 mpbid ralrimivv syl3anbrc
        syl dff1o6 f1oeq1 syl5ibr ax-mp ) CAEBAJZDKZUAZLZDUBMZBEMZNZEECOZPFWJWK
        WGEEWFOZWJWFEUDZWFUEELHJZWFQZIJZWFQZLZHIUFZPZIERHERWLWJWEUGMZAERZWMXBWJ
        XAAEBWDDUHUCUIAEWEWFWFUJZUKULABWFDEXCGUMWJWTHIEEWJWNEMZWPEMZNZWRWSWJXFW
        RUQZBWNDKZBWPDKZLZWSXGXHWOWQXIWJXFXHWOLWRWJXFNZWOXHXKXDXHEMZWOXHLWJXDXE
        UNWHWIXFXLXFWHWIXLXDWHWIXLPPXEWHWIXDXLWHWIXDXLBWNDEGUOSURUPUSUTAWNWEXHE
        EWFWDWNBDVAXCVBVCVDTWJXFWRVHXGXEXIEMZNZWQXILWJXFXNWRXKXEXMWJXDXEVEWJXFX
        MWJXEXMXDWHWIXEXMBWPDEGUOVFVGVIVJTAWPWEXIEEWFWDWPBDVAXCVBVSVKWJXFXJWSVL
        ZWRWHWIXFXOXFWHWIXOXDXEWHWIXOPPXDXEWIWHXOXDXEWIWHXOPWHXDXEWIUQXOWNWPBDE
        GVMVNSVOVIUSUTTVPSVQHIEEWFVTVREECWFWAWBWC $.
        $( [29-Apr-2012] $)
    $}

    ${
      $d A x y $.  $d G x y $.  $d X x y $.
      $( Left translation expressed as a composite.  (Contributed by FL,
         3-Jul-2012.) $)
      ltrcmp $p |- ( ( G e. GrpOp /\ A e. X ) ->
        F = ( G o. { <. x , y >. | ( x e. X /\ y = <. A , x >. ) } ) ) $=
        ( cgr wcel wa cv cop cxp wral wfn wceq copab ccom opelxpi ralrimiva wfo
        adantll grpofo fofn syl adantr eqid cmpt cfv df-mpt df-ov eqeq2i anbi2i
        co opabbii 3eqtri fnopabco2b syl2anc ) EIJZCFJZKZCALZMZFFNZJZAFOEVEPZDE
        VCFJZBLZVDQKABRZSQVBVFAFVAVHVFUTCVCFFTUCUAUTVGVAUTVEFEUBVGEFHUDVEFEUEUF
        UGABFVDVEVJDEVJUHDAFCVCEUOZUIVHVIVKQZKZABRVHVIVDEUJZQZKZABRGABFVKUKVMVP
        ABVLVOVHVKVNVICVCEULUMUNUPUQURUS $.
        $( [3-Jul-2012] $)
    $}

    ${
      $d A x y z $.  $d F z $.  $d G x y z $.  $d N x y z $.  $d X x y z $.
      ltrinvlem.2 $e |- D = ( /g ` G ) $.
      ltrinvlem.3 $e |- N = ( inv ` G ) $.
      $( The converse of a left translation.  The term ` A ` is a constant.
         (Contributed by FL, 30-Apr-2012.) $)
      ltrinvlem $p |- ( ( G e. GrpOp /\ A e. X )
          -> `' F = ( x e. X |-> ( ( N ` A ) G x ) ) ) $=
        ( vy vz wcel wa cfv cv co cmpt wceq ccnv wral wf1o ltrooo adantr simpll
        cgr grpoinvcl simpr grpocl syl3anc jca simplr oveq2 cbvmptv syl2anc w3a
        fvmptg grpoass eqcomd syl13anc cgi eqid grporinv oveq1d grpolid adantlr
        eqtri eqtrd 3eqtrd f1ocnvfv fvmpt2 eqtr4d ralrimiva wfn wb f1ocnv f1ofn
        sylc 3syl syldan syl wal wi hbth cnveq hbmpt1 hbcnv a1i hbxfrd eqfnfv2f
        ax-mp mpbird ) EUGNZBGNZOZDUAZAGBFPZAQZERZSZTZWSWQPZWSXAPZTZAGUBZWPXEAG
        WPWSGNZOZXCWTXDXHGGDUCZWTGNZOWTDPZWSTXCWTTXHXIXJWPXIXGABDEGHIUDZUEXHWNW
        RGNZXGXJWNWOXGUFZWPXMXGBEFGIKUHZUEZWPXGUIZWRWSEGIUJUKZULXHXKBWTERZBWRER
        ZWSERZWSXHXJXSGNZXKXSTXRXHWNWOXJYBXNWNWOXGUMZXRBWTEGIUJUKLWTBLQZERZXSGG
        DYDWTBEUNDAGBWSERZSZLGYESHALGYFYEWSYDBEUNUOVHURUPXHWNWOXMXGXSYATXNYCXPX
        QWNWOXMXGUQOYAXSBWRWSEGIUSUTVAXHYAEVBPZWSERZWSXHXTYHWSEWPXTYHTXGBYHEFGI
        YHVCZKVDUEVEWNXGYIWSTWOWSYHEGIYJVFVGVIVJGGWTWSDVKVSXHXGXJXDWTTXQXRAGWTG
        XAXAVCZVLUPVMVNWPWQGVOZXAGVOZXBXFVPWPXIGGWQUCYLXLGGDVQGGWQVRVTWPGGXAUCZ
        YMWNWOXMYNXOAWRXAEGYKIUDWAGGXAVRWBAMGWQXADYGTZMQZWQNZYQAWCWDHYOAMWQYGUA
        ZYOAHWEDYGWFYPYRNZYSAWCWDYOAMYGAMGYFWGWHWIWJWLAMGWTWGWKUPWM $.
        $( [30-Apr-2012] $)
    $}

    ${
      $d A v x y z $.  $d B v w x y z $.  $d F z $.  $d G v w x y z $.
      $d X v w x y z $.
      cmpltr.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  Don't use.  See ~ cmpltr2 .  (Contributed by FL,
         2-Jul-2012.) $)
      cmpltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vz cgr wcel cv co cmpt wceq oveq2 cbvmptv a1i w3a ccom 3expa 3adantl2
        grpocl eqtri fmptco grpoass 3exp2 3imp1 mpteq2dva oveq2d syl6req eqtrd
        ) ELMZBGMZCGMZUAZDFUBKGBCKNZEOZEOZPZAGBCEOANZEOZPZURKAGGUTBVCEOZVAFDUOU
        QUSGMZUTGMZUPUOUQVGVHCUSEGIUEUCUDFKGUTPZQURFAGCVCEOZPVIJAKGVJUTVCUSCERZ
        SUFTDAGVFPQURHTVCUTBERUGURVEAGBVJEOZPVBURAGVDVLUOUPUQVCGMZVDVLQZUOUPUQV
        MVNBCVCEGIUHUIUJUKAKGVLVAVCUSQVJUTBEVKULSUMUN $.
        $( [2-Jun-2014] $) $( [2-Jul-2012] $)

    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      cmpltr2.2 $e |- H = ( x e. X |-> ( B G x ) ) $.
      $( Composite of two left translations.  The terms ` A ` and ` B ` are
         constant.  (Contributed by FL, 2-Jul-2012.) $)
      cmpltr2 $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
           ( F o. H ) = ( x e. X |-> ( ( A G B ) G x ) ) ) $=
        ( vy cgr wcel w3a co cv cmpt oveq2 cbvmptv eqtri ccom cmpltr syl6eq ) E
        LMBGMCGMNDFUAKGBCEOZKPZEOZQAGUDAPZEOZQKBCDEFGDAGBUGEOZQKGBUEEOZQHAKGUIU
        JUGUEBERSTIFAGCUGEOZQKGCUEEOZQJAKGUKULUGUECERSTUBKAGUFUHUEUGUDERSUC $.
        $( [2-Jul-2012] $)
    $}
  $}

  ${
    $d A x y $.  $d B x y $.  $d E x $.  $d G x y $.  $d X x y $.
    cmperltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    cmperltr.2 $e |- E = ( y e. X |-> ( A G y ) ) $.
    cmperltr.3 $e |- H = ( x e. X |-> ( x G B ) ) $.
    cmperltr.4 $e |- X = ran G $.
    $( A right and left translation expressed as a composite.  Note that ` x `
       and ` y ` can't be the same.  (Contributed by FL, 2-Jul-2012.) $)
    cmperltr $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
      -> F = ( E o. H ) ) $=
      ( cgr wcel cv co cmpt wceq a1i grpoass 3exp2 com34 3imp1 mpteq2dva syl5eq
      w3a ccom grpocl 3expa an32s 3adantl2 oveq2 fmptco eqtr4d ) GNOZCIOZDIOZUG
      ZFAICAPZDGQZGQZRZEHUHUSFAICUTGQDGQZRVCJUSAIVDVBUPUQURUTIOZVDVBSZUPUQVEURV
      FUPUQVEURVFCUTDGIMUAUBUCUDUEUFUSABIIVACBPZGQZVBHEUPURVEVAIOZUQUPVEURVIUPV
      EURVIUTDGIMUIUJUKULHAIVARSUSLTEBIVHRSUSKTVGVACGUMUNUO $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.  $d F x $.  $d G x y $.
    $d X x y $.
    cmprltr.1 $e |- F = ( y e. X |-> ( ( A G y ) G C ) ) $.
    cmprltr.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  Note that ` x ` and ` y `
       can't be the same.  See ~ cmprltr2 for a more general version.
       (Contributed by FL, 2-Jul-2012.)  (The proof was shortened by Mario
       Carneiro, 26-Jul-2014.) $)
    cmprltr $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( wcel wa co cmpt grpocl syl3anc wceq cgr w3a ccom simpl1 simpl2r simpl3r
      cv simpr a1i oveq2 oveq1d fmptco simpl2l grpoass syl13anc eqtrd mpteq2dva
      simpl3l eqtr3d ) IUANZCJNZDJNZOZEJNZFJNZOZUBZHGUCAJCDAUGZIPZFIPZIPZEIPZQA
      JCDIPZVHIPZFEIPIPZQVGABJJVJCBUGZIPZEIPZVLGHVGVHJNZOZUTVIJNZVEVJJNUTVCVFVS
      UDZVTUTVBVSWAWBVAVBUTVFVSUEZVGVSUHZDVHIJMRSZVDVEUTVCVSUFZVIFIJMRSGAJVJQTV
      GLUIHBJVRQTVGKUIVPVJTVQVKEIVPVJCIUJUKULVGAJVLVOVTVNFIPZEIPZVLVOVTWGVKEIVT
      WGCVIIPZFIPZVKVTVNWIFIVTUTVAVBVSVNWITWBVAVBUTVFVSUMZWCWDCDVHIJMUNUOUKVTUT
      VAWAVEWJVKTWBWKWEWFCVIFIJMUNUOUPUKVTUTVNJNZVEVDWHVOTWBVTUTVMJNZVSWLWBVTUT
      VAVBWMWBWKWCCDIJMRSWDVMVHIJMRSWFVDVEUTVCVSURVNFEIJMUNUOUSUQUP $.
      $( [2-Jul-2012] $)
  $}

  ${
    $d A a u x $.  $d A u z $.  $d B a u x $.  $d B u z $.  $d C a u x $.
    $d C u z $.  $d D a u x $.  $d D u z $.  $d F u $.  $d G a u x $.
    $d G u z $.  $d X u x $.  $d X u z $.
    cmprltr2.1 $e |- F = ( z e. X |-> ( ( A G z ) G C ) ) $.
    cmprltr2.2 $e |- E = ( x e. X |-> ( ( B G x ) G D ) ) $.
    cmprltr2.3 $e |- X = ran G $.
    $( Composite of two right and left translations.  No restriction: ` x ` and
       ` z ` can be equal.  (Contributed by FL, 2-Jul-2012.) $)
    cmprltr2 $p |- ( ( G e. GrpOp /\
      ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) -> ( F o. E ) =
         ( x e. X |-> ( ( ( A G B ) G x ) G ( D G C ) ) ) ) $=
      ( vu wcel wa co cv cmpt weq cgr oveq2 oveq1d cbvmptv eqtri cmprltr syl6eq
      w3a ccom ) IUAOCJODJOPEJOFJOPUHHGUINJCDIQZNRZIQZFEIQZIQZSAJUJARZIQZUMIQZS
      NBCDEFGHIJKGAJDUOIQZFIQZSNJDUKIQZFIQZSLANJUSVAANTURUTFIUOUKDIUBUCUDUEMUFN
      AJUNUQNATULUPUMIUKUOUJIUBUCUDUG $.
      $( [2-Jul-2012] $)
  $}

  ${
    rltr.1 $e |- F = ( x e. X |-> ( ( A G x ) G B ) ) $.
    rltr.2 $e |- X = ran G $.

    ${
      $d X x $.
      $( The domain of a right and left translation.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrdom $p |- dom F = X $=
        ( cv co cmpdom2 ) AFBAIEJCDEGK $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d X x $.
      $( A right and left translation is a set.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrset $p |- ( G e. GrpOp -> F e. _V ) $=
        ( cgr wcel wfun cdm cvv cv co cmpfun rltrdom unsgrp syl5eqel funex
        sylancr ) EIJZDKDLZMJDMJAFBANEOCEODGPUBUCFMABCDEFGHQEFHRSMDTUA $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A x y $.  $d B x y $.  $d G x y $.  $d X x y $.
      $( The range of a right and left translation.  Note that ` A ` and ` B `
         are constant.  (Contributed by FL, 2-Jul-2012.) $)
      rltrran $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) -> ran F = X ) $=
        ( vy wcel w3a co wceq wa grpocl syl3anc cfv syl2anc eqcomd oveq1d rnmpt
        cgr crn cv wrex wi simpr1 simpr2 simpl simpr3 3jca ex syl6 eleq1 imbi2d
        cab syl5ibrcom rexlimiv com12 cgn simpl1 grpoinvcl 3adant3 adantr simpr
        eqid 3adant2 simpl3 grpoass syl13anc grpolinv oveq2d 3ad2antl1 3eqtr2rd
        cgi grporid grpolid simpl2 grporinv 3eqtrd eqtrd eqeq2d rcla4ev abbi1dv
        oveq2 impbid syl5eq ) EUBJZBFJZCFJZKZDUCIUDZBAUDZELZCELZMZAFUEZIUPFAIFW
        ODGUAWKWQIFWKWQWLFJZWQWKWRWPWKWRUFZAFWMFJZWSWPWKWOFJZUFWTWKWHWNFJZWJKZX
        AWTWKXCWTWKNZWHXBWJWTWHWIWJUGZXDWHWIWTXBXEWTWHWIWJUHWTWKUIBWMEFHOPWTWHW
        IWJUJUKULWNCEFHOUMWPWRXAWKWLWOFUNUOUQURUSWKWRWQWKWRNZBEUTQZQZWLCXGQZELZ
        ELZFJZWLBXKELZCELZMZWQXFWHXHFJZXJFJZXLWHWIWJWRVAZWKXPWRWHWIXPWJBEXGFHXG
        VFZVBVCVDZXFWHWRXIFJZXQXRWKWRVEZWKYAWRWHWJYAWICEXGFHXSVBVGVDZWLXIEFHOPZ
        XHXJEFHOPXFWLBXHELZXJELZCELZXNXFWLXJCELZEVOQZXJELZCELYGXFYHWLXICELZELZW
        LYIELZWLXFWHWRYAWJYHYLMXRYBYCWHWIWJWRVHZWLXICEFHVIVJXFYIYKWLEXFYKYIXFWH
        WJYKYIMXRYNCYIEXGFHYIVFZXSVKRSVLWHWIWRYMWLMWJWLYIEFHYOVPVMVNXFXJYJCEXFW
        HXQXJYJMXRYDWHXQNYJXJXJYIEFHYOVQSRTXFYJYFCEXFYIYEXJEXFYEYIXFWHWIYEYIMXR
        WHWIWJWRVRZBYIEXGFHYOXSVSRSTTVTXFYFXMCEXFWHWIXPXQYFXMMXRYPXTYDBXHXJEFHV
        IVJTWAWPXOAXKFWMXKMZWOXNWLYQWNXMCEWMXKBEWETWBWCRULWFWDWG $.
        $( [2-Jul-2012] $)
    $}

    ${
      $d A u x y $.  $d B u x y $.  $d G u x y $.  $d X u x y $.
      $( A right and left translation is a bijection.  (Contributed by FL,
         2-Jul-2012.) $)
      rltrooo $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X )
           -> F : X -1-1-onto-> X ) $=
        ( vy vu cv co wceq wcel w3a wf1o wi cfv wral wa cmpt cgr wfn ovex rgenw
        crn weq cvv a1i mptfng sylib rltrran fvmpt2 mpan2 ad2antrl oveq2 oveq1d
        eqid fvmpt ad2antll eqeq12d simprl1 simpl1 simpl2 simprl grpocl syl3anc
        simpl simprr simpl3 3jca grporcan syl2anc mpbid simprrl simprrr simprl2
        wb adantl grpolcan syl13anc ex syl6bi pm2.43a ralrimivva hbmpt1 dff1o6f
        ax-17 syl3anbrc f1oeq1 syl5ibr ax-mp ) DAFBAKZELZCELZUAZMZEUBNZBFNZCFNZ
        OZFFDPZQGXAXBWQFFWPPZXAWPFUCZWPUFFMWMWPRZIKZWPRZMZAIUGZQZIFSAFSXCXAWOUH
        NZAFSZXDXLXAXKAFWNCEUDZUEUIAFWOWPWPURZUJUKABCWPEFXNHULXAXJAIFFXHXAWMFNZ
        XFFNZTZTZXIXRXHWOBXFELZCELZMZXRXIQXRXEWOXGXTXOXEWOMZXAXPXOXKYBXMAFWOUHW
        PXNUMUNUOXPXGXTMXAXOAXFWOXTFWPXIWNXSCEWMXFBEUPUQXNXSCEUDUSUTVAYAXRXIYAX
        RTZWNXSMZXIYCYAYDYAXRVHYCWRWNFNZXSFNZWTOZYAYDVRWRWSWTXQYAVBZXRYGYAXRYEY
        FWTXRWRWSXOYEWRWSWTXQVCZWRWSWTXQVDZXAXOXPVEBWMEFHVFVGXRWRWSXPYFYIYJXAXO
        XPVIBXFEFHVFVGWRWSWTXQVJVKVSWNXSCEFHVLVMVNYCWRXOXPWSYDXIVRYHYAXAXOXPVOY
        AXAXOXPVPWRWSWTXQYAVQWMXFBEFHVTWAVNWBWCWDWEAIJFFWPAJFWOWFJKWPNIWHWGWIFF
        DWPWJWKWL $.
        $( [2-Jul-2012] $)
    $}
  $}

  $( Compare fnopab2g and fnopab2 . If an antecedent, the condition B e. _V
     must be quantified. If a premisse it needn't. $)


