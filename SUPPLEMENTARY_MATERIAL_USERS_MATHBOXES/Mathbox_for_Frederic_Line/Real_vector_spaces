$(
  df-LAlg @a |- LAlg = { f e. LMod | (
    k = ( Base ` ( Scalar ` f ) )
    v = ( Base ` f )
    a = ( +g ` f )
    s = ( vsca ` f )
    m = ( vmul ` f )
    A. q e. k A. x e. v A. y e. v A. z e. v
          ( x m y ) e. v
          ( ( q s x ) m y ) = ( q s ( x m y ) )
          ( ( q s x ) m y ) = ( x m ( q s y ) )
          ( ( x a y ) m z ) = ( ( x m z ) a ( y m z ) )
          ( x m ( y a z ) ) = ( ( x m y ) a ( x m z ) )
  }
$)
$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Real vector spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c RVec $.

  $( Extend class notation with the class of all real vector spaces. $)
  cvr $a class RVec $.

  ${
    $d g s x y z $.
    $( Define the class of all real vector spaces.  The definition of a
       ` RVec ` and a ` CVec ` don't much differ.  There may be a way to get
       both in only one definition.  A ` RVec ` seems mandatory if one wants to
       do classical cartesian geometry.  We can't use a ` CVec ` instead.
       Changing the field changes important properties such as the
       dimension. $)
    df-vr $a |- RVec = { <. g , s >. | ( g e. AbelOp /\
     s : ( RR X. ran g ) --> ran g /\ A. x e. ran g ( ( 1 s x ) = x /\
       A. y e. RR ( A. z e. ran g ( y s ( x g z ) ) = ( ( y s x ) g ( y s z ) )
         /\ A. z e. RR ( ( ( y + z ) s x ) = ( ( y s x ) g ( z s x ) ) /\
           ( ( y x. z ) s x ) = ( y s ( z s x ) ) ) ) ) ) } $.

    $( The class of all real vector spaces is a relation.  (Contributed by FL,
       16-Nov-2008.) $)
    vrrel $p |- Rel RVec $=
      ( vg vs vx vy vz cv cablo wcel cr crn cxp wf c1 co wceq wral caddc wa w3a
      cmul cvr df-vr relopabi ) AFZGHIUDJZKUEBFZLMCFZUFNUGODFZUGEFZUDNUFNUHUGUF
      NZUHUIUFNUDNOEUEPUHUIQNUGUFNUJUIUGUFNZUDNOUHUITNUGUFNUHUKUFNOREIPRDIPRCUE
      PSABUACDEABUBUC $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)
  $}

  ${
    $d G g s x y z $.  $d S g s x y z $.  $d W g s $.  $d X g s x z $.
    vri.1 $e |- G = ( 1st ` W ) $.
    vri.2 $e |- S = ( 2nd ` W ) $.
    vri.3 $e |- X = ran G $.
    $( The properties of a real vector space, which is an abelian group (i.e.
       the vectors, with the operation of vector addition) accompanied by a
       scalar multiplication operation on the field of real numbers.  The
       variable ` W ` was chosen because ` _V ` is already used for the
       universal class.  (Contributed by FL, 16-Nov-2008.) $)
    vri $p |- ( W e. RVec ->
              ( G e. AbelOp /\
         S : ( RR X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. RR ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. RR ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) $=
      ( vg vs cr cv co wceq wral wa oveq ralbidv cablo wcel cxp wf c1 caddc w3a
      cmul crn copab cvr c1st cfv wb eqeq2i eleq1 rneq syl6eqr eqidd xpeq2 3jca
      id feq123 oveq2d eqeq12d raleqbidv eqeq2d anbi1d anbi12d anbi2d 3anbi123d
      3syl sylbir c2nd feq1 eqeq1d oveq12d eqtrd 3anbi23d elopabi df-vr eleq2s
      ) EUAUBZMGUCZGDUDZUEANZDOZWFPZBNZWFCNZEOZDOZWIWFDOZWIWJDOZEOZPZCGQZWIWJUF
      OZWFDOZWMWJWFDOZEOZPZWIWJUHOZWFDOZWIWTDOZPZRZCMQZRZBMQZRZAGQZUGZFKNZUAUBZ
      MXNUIZUCZXPLNZUDZUEWFXROZWFPZWIWFWJXNOZXROZWIWFXROZWIWJXROZXNOZPZCXPQZWRW
      FXROZYDWJWFXROZXNOZPZXCWFXROZWIYJXROZPZRZCMQZRZBMQZRZAXPQZUGZKLUJUKUUBWCW
      DGXRUDZYAWIWKXROZYDYEEOZPZCGQZYIYDYJEOZPZYORZCMQZRZBMQZRZAGQZUGZXMKLFXNFU
      LUMZPXNEPZUUBUUPUNEUUQXNHUOUURXOWCXSUUCUUAUUOXNEUAUPUURXPGPZXRXRPZXQWDPZU
      USUGXSUUCUNUURXPEUIGXNEUQJURZUUSUUTUVAUUSUUSXRUSXPGMUTUUSVBVAXQXPWDGXRXRV
      CVLUURYTUUNAXPGUVBUURYSUUMYAUURYRUULBMUURYHUUGYQUUKUURYGUUFCXPGUVBUURYCUU
      DYFUUEUURYBWKWIXRWFWJXNESVDYDYEXNESVEVFUURYPUUJCMUURYLUUIYOUURYKUUHYIYDYJ
      XNESVGVHTVITVJVFVKVMXRFVNUMZPXRDPZUUPXMUNDUVCXRIUOUVDUUCWEUUOXLWCWDGXRDVO
      UVDUUNXKAGUVDYAWHUUMXJUVDXTWGWFUEWFXRDSVPUVDUULXIBMUVDUUGWQUUKXHUVDUUFWPC
      GUVDUUDWLUUEWOWIWKXRDSUVDYDWMYEWNEWIWFXRDSZWIWJXRDSVQVETUVDUUJXGCMUVDUUIX
      BYOXFUVDYIWSUUHXAWRWFXRDSUVDYDWMYJWTEUVEWJWFXRDSZVQVEUVDYMXDYNXEXCWFXRDSU
      VDYNWIWTXROXEUVDYJWTWIXRUVFVDWIWTXRDSVRVEVITVITVITVSVMVTABCKLWAWB $.
      $( [17-Nov-2008] $) $( [16-Nov-2008] $)
  $}

