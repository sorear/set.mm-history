$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Homeomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( In this section every @d statement has been checked. 10-Feb-2008 $)

  ${
    $d F x y $.  $d G x y $.  $d J x y $.  $d K x y $.  $d L x y $.
    $( The composite of two homeomorphisms is a homeomorphism.  (Contributed by
       FL, 9-Mar-2007.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    cmphmp $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
     ( ( F e. ( J Homeo K ) /\ G e. ( K Homeo L ) ) ->
     ( G o. F ) e. ( J Homeo L ) ) ) $=
      ( ctop wcel chomeosm co wa ccom ccn ccnv hmeobc syl2anc mpbid simpld cnco
      wb syl32anc w3a 3adant2 adantr simpl1 simpl2 simpl3 simprl cnvco syl5eqel
      simprr simprd mpbir2and ex ) CFGZDFGZEFGZUAZACDHIGZBDEHIGZJZBAKZCEHIGZUQU
      TJZVBVACELIGZVAMZECLIZGZUQVBVDVGJSZUTUNUPVHUOVACENUBUCVCUNUOUPACDLIGZBDEL
      IGZVDUNUOUPUTUDZUNUOUPUTUEZUNUOUPUTUFZVCVIAMZDCLIGZVCURVIVOJZUQURUSUGVCUN
      UOURVPSVKVLACDNOPZQVCVJBMZEDLIGZVCUSVJVSJZUQURUSUJVCUOUPUSVTSVLVMBDENOPZQ
      ABCDERTVCVEVNVRKZVFBAUHVCUPUOUNVSVOWBVFGVMVLVKVCVJVSWAUKVCVIVOVQUKVRVNEDC
      RTUIULUM $.
      $( [30-May-2014] $) $( [9-Mar-2007] $)
  $}

  ${
    $d J x $.  $d X x $.
    idhme.1 $e |- X = U. J $.
    $( The identity function is a homeomorphism.  (Contributed by FL,
       14-Feb-2007.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    idhme $p |- ( J e. Top -> ( _I |` X ) e. ( J Homeo J ) ) $=
      ( ctop wcel cid cres chomeosm co ccn ccnv hmeobc anidms cnvresid syl5eqel
      wa wb idcn mpbir2and ) ADEZFBGZAAHIEZUAAAJIZEZUAKZUCEZTUBUDUFPQUAAALMABCR
      ZTUEUAUCBNUGOS $.
      $( [30-May-2014] $) $( [14-Feb-2007] $)
  $}

  ${
    $d F x $.  $d J x $.  $d K x $.
    $( The converse of a homeomorphism is a homeomorphism.  (Contributed by FL,
       5-Mar-2007.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.) $)
    cnvhmph $p |- ( ( J e. Top /\ K e. Top /\ Rel F ) ->
      ( `' F e. ( J Homeo K ) <-> F e. ( K Homeo J ) ) ) $=
      ( ctop wcel wrel w3a ccnv chomeosm co cnvhmpha 3adant3 simp3 dfrel2 sylib
      wi wceq eleq1d sylibd ancoms impbid ) BDEZCDEZAFZGZAHZBCIJEZACBIJZEZUEUGU
      FHZUHEZUIUBUCUGUKPUDUFBCKLUEUJAUHUEUDUJAQUBUCUDMANORSUBUCUIUGPZUDUCUBULAC
      BKTLUA $.
      $( [30-May-2014] $) $( [5-Mar-2007] $)
  $}

  ${
    $d J f g $.  $d J f x $.  $d K f g $.  $d K f x $.
    $( "Is homeomorph to" is symmetric.  (Contributed by FL, 8-Mar-2007.)  (The
       proof was shortened by Mario Carneiro, 30-May-2014.) $)
    hmphsyma $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> K ~= J ) ) $=
      ( vf ctop wcel wa chomeosm co c0 wne chomeo wbr cv wex ccnv cnvhmpha ne0i
      n0 syl6 hmph exlimdv syl5bi wb ancoms 3imtr4d ) ADEZBDEZFZABGHZIJZBAGHZIJ
      ZABKLBAKLZUJCMZUIEZCNUHULCUIRUHUOULCUHUOUNOZUKEULUNABPUKUPQSUAUBABTUGUFUM
      ULUCBATUDUE $.
      $( [30-May-2014] $) $( [8-Mar-2007] $)
  $}

  ${
    $( "Is homeomorph to" is symmetric.  (Contributed by FL, 22-Feb-2007.) $)
    hmphsym $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K <-> K ~= J ) ) $=
      ( ctop wcel wa chomeo wbr hmphsyma wi ancoms impbid ) ACDZBCDZEABFGZBAFGZ
      ABHMLONIBAHJK $.
      $( [11-Mar-2007] $) $( [22-Feb-2007] $)
  $}

  ${
    $d J f $.
    $( "Is homeomorph to" is reflexive.  (Contributed by FL, 25-Feb-2007.)
       (The proof was shortened by Mario Carneiro, 30-May-2014.) $)
    hmphre $p |- ( J e. Top -> J ~= J ) $=
      ( ctop wcel chomeo wbr chomeosm co c0 wne cid cuni cres eqid idhme syl wb
      ne0i hmph anidms mpbird ) ABCZAADEZAAFGZHIZUAJAKZLZUCCUDAUEUEMNUCUFQOUAUB
      UDPAARST $.
      $( [30-May-2014] $) $( [25-Feb-2007] $)
  $}

  ${
    $d J f g h $.  $d K f g $.  $d L f g h $.
    $( "Is homeomorph to" is transitive.  (Contributed by FL, 9-Mar-2007.) $)
    hmphtr $p |- ( ( J e. Top /\ K e. Top /\ L e. Top ) ->
      ( ( J ~= K /\ K ~= L ) -> J ~= L ) ) $=
      ( vf vg ctop wcel w3a chomeo wbr wa chomeosm co c0 wne wb hmph cv wex n0
      3adant3 3adant1 anbi12d anbi12i eeanv bitr4i ccom cmphmp 3adant2 exlimdvv
      ne0i syl6 sylibrd syl5bi sylbid ) AFGZBFGZCFGZHZABIJZBCIJZKABLMZNOZBCLMZN
      OZKZACIJZUSUTVCVAVEUPUQUTVCPURABQUAUQURVAVEPUPBCQUBUCVFDRZVBGZERZVDGZKZES
      DSZUSVGVFVIDSZVKESZKVMVCVNVEVODVBTEVDTUDVIVKDEUEUFUSVLVGDEUSVLACLMZNOZVGU
      SVLVJVHUGZVPGVQVHVJABCUHVPVRUKULUPURVGVQPUQACQUIUMUJUNUO $.
      $( [30-May-2014] $) $( [9-Mar-2007] $)
  $}

  ${
    $d x y z $.
    $( ` ~= ` is a relation whose domain is included in ` Top ` .  (Contributed
       by FL, 23-Mar-2007.) $)
    dmhmph $p |- dom ~= C_ Top $=
      ( chomeo cdm ctop cxp wss chomeosm ccnv c0 csn cima df-hmph difss eqsstri
      cdif dmss ax-mp dmxpss sstri ) ABZCCDZBZCATESUAEATFGHIJZNTKTUBLMATOPCCQR
      $.
      $( [30-May-2014] $) $( [23-Mar-2007] $)

    $( ` ~= ` is a relation whose range is included in ` Top ` .  (Contributed
       by FL, 23-Mar-2007.) $)
    rnhmph $p |- ran ~= C_ Top $=
      ( chomeo crn ctop cxp wss chomeosm ccnv c0 csn cima df-hmph difss eqsstri
      cdif rnss ax-mp rnxpss sstri ) ABZCCDZBZCATESUAEATFGHIJZNTKTUBLMATOPCCQR
      $.
      $( [30-May-2014] $) $( [23-Mar-2007] $)
  $}

  ${
    $( The relation "being homeomorph to" implies the operands are topologies.
       (Contributed by FL, 23-Mar-2007.)  (Revised by Mario Carneiro,
       30-May-2014.) $)
    dmhmpha $p |- ( A ~= B -> A e. Top ) $=
      ( chomeo wbr cdm ctop dmhmph wrel wcel cxp wss chomeosm ccnv c0 cima cdif
      csn df-hmph difss eqsstri relxp relss mp2 releldm mpan sseldi ) ABCDZCEZF
      AGCHZUGAUHICFFJZKUJHUICUJLMNQOZPUJRUJUKSTFFUACUJUBUCABCUDUEUF $.
      $( [30-May-2014] $) $( [23-Mar-2007] $)

    rnhmpha.1 $e |- B e. _V $.
    $( The relation "being homeomorph to" implies the operands are topologies.
       (Contributed by FL, 23-Mar-2007.)  (Revised by Mario Carneiro,
       30-May-2014.) $)
    rnhmpha $p |- ( A ~= B -> B e. Top ) $=
      ( chomeo wbr crn ctop rnhmph wcel dmhmpha brelrng mp3an2 mpancom sseldi
      cvv ) ABDEZDFZGBHAGIZPBQIZABJRBOIPSCABDGOKLMN $.
      $( [30-May-2014] $) $( [23-Mar-2007] $)
  $}


  ${
    $d x y z $.
    $( "Is homeomorph to" is an equivalence relation.  (Contributed by FL,
       22-Mar-2007.) $)
    hmpher $p |- Er ~= $=
      ( vx vy vz chomeo cv ctop wcel wbr dmhmpha vex rnhmpha jca hmphsyma mpcom
      wa w3a adantr adantl 3jca hmphtr iser ) ABCDAEZFGZBEZFGZOUBUDDHZUDUBDHUFU
      CUEUBUDIZUBUDBJKZLUBUDMNUCUECEZFGZPUFUDUIDHZOZUBUIDHULUCUEUJUFUCUKUGQUFUE
      UKUHQUKUJUFUDUICJKRSUBUDUITNUA $.
      $( [30-May-2014] $) $( [22-Mar-2007] $)
  $}

  ${
    hmphsymv.1 $e |- A e. _V $.
    hmphsymv.2 $e |- B e. _V $.
    $( A more general version of ~ hmphsym .  Certainly not a useful proof
       (since it's a simple consequence of ~ hmpher ) but it shows that the
       conditions of ~ hmphsym could (and should) be weakened.  (Contributed by
       FL, 23-Mar-2007.) $)
    hmphsymv $p |- ( A ~= B <-> B ~= A ) $=
      ( chomeo wbr wb wtru cvv wer hmpher a1i wcel ersymb trud ) ABEFBAEFGHABEI
      IEJHKLAIMHCLBIMHDLNO $.
      $( [23-Mar-2007] $)

  $}

  ${
    $d x y A $.  $d y B $.  $d f g h G $.  $d f g h x y J $.
    hmeogrp.1 $e |- G = ( x e. ( J Homeo J ) , y e. ( J Homeo J ) |->
                          ( x o. y ) ) $.
    $( Lemma for ~ hmeogrp . $)
    hmeogrplem $p |- ( ( A e. ( J Homeo J ) /\ B e. ( J Homeo J ) ) ->
                       ( A G B ) = ( A o. B ) ) $=
      ( chomeosm co wcel ccom cvv wceq coexg cv coeq1 coeq2 ovmpt2g mpd3an3 ) C
      FFHIZJDTJCDKZLJCDEIUAMCDTTNABCDTTAOZBOZKUAECUCKLUBCUCPUCDCQGRS $.
      $( [30-May-2014] $)

    ${
      hmeogrpi.2 $e |- J e. Top $.
      $( Lemma for ~ hmeogrp . $)
      hmeogrpi $p |- G e. GrpOp $=
        ( vf vg vh cv co ccom wcel wa wi cmphmp mp3an wceq syl2anc hmeogrplem
        cid cuni cres ccnv chomeosm ovex wral cxp ctop ancoms rgen2a fmpt2 mpbi
        w3a coass simp2 simp1 simp3 3eqtr4a 3adant3 oveq1d 3adant1 3eqtr4d eqid
        wf oveq2d idhme ax-mp mpan wf1o hmeomap mp2an f1of fcoi2 eqtrd cnvhmpha
        3syl mpancom f1ococnv1 syl isgrpoi ) GHIUADUBZUCZCGJZUDZDDUEKZDDUEUFAJZ
        BJZLZWFMZBWFUGAWFUGWFWFUHWFCVEWJABWFWHWFMZWGWFMZWJDUIMZWMWMWKWLNWJOFFFW
        HWGDDDPQUJUKABWFWFWIWFCEULUMWDWFMZHJZWFMZIJZWFMZUNZWDWOLZWQCKZWDWOWQLZC
        KZWDWOCKZWQCKWDWOWQCKZCKWSWTWQLZWDXBLZXAXCWDWOWQUOWSWTWFMZWRXAXFRWSWPWN
        XHWNWPWRUPZWNWPWRUQZWMWMWMWPWNNXHOFFFWOWDDDDPQSWNWPWRURZABWTWQCDETSWSWN
        XBWFMZXCXGRXJWSWRWPXLXKXIWMWMWMWRWPNXLOFFFWQWODDDPQSABWDXBCDETSUSWSXDWT
        WQCWNWPXDWTRWRABWDWOCDETUTVAWSXEXBWDCWPWRXEXBRWNABWOWQCDETVBVFVCWMWCWFM
        ZFDWBWBVDZVGVHZWNWCWDCKZWCWDLZWDXMWNXPXQRXOABWCWDCDETVIWNWBWBWDVJZWBWBW
        DVEXQWDRWMWMWNXROFFWDDDWBWBXNXNVKVLZWBWBWDVMWBWBWDVNVQVOWMWMWNWEWFMZOFF
        WDDDVPVLZWNWEWDCKZWEWDLZWCXTWNYBYCRYAABWEWDCDETVRWNXRYCWCRXSWBWBWDVSVTV
        OWA $.
        $( [31-May-2014] $)
    $}

    $( Homeomorphisms on a topology ` J ` is a group for composition.  This
       means from Felix Klein's point of view that a set equipped with a
       topology is a geometry, namely the so-called rubber sheet geometry.
       (Contributed by FL, 3-Feb-2008.)  (The proof was shortened by Mario
       Carneiro, 31-May-2014.) $)
    hmeogrp $p |- ( J e. Top -> G e. GrpOp ) $=
      ( ctop wcel cgr c0 csn cif chomeosm co cv ccom cmpt2 wceq oveq12 mpt2eq12
      anidms syl2anc syl5eq eleq1d eqid sn0top elimel hmeogrpi dedth ) DFGZCHGA
      BUIDIJZKZUKLMZULANBNOZPZHGDUJDUKQZCUNHUOCABDDLMZUPUMPZUNEUOUPULQZURUQUNQU
      OURDUKDUKLRTZUSABUPUPULULUMSUAUBUCABUNUKUNUDDUJFUEUFUGUH $.
      $( [15-Dec-2013] $) $( [3-Feb-2008] $)
  $}

  ${
    $d f u v J $.  $d f u v K $.
    $( Lemma for ~ homcard . $)
    homcardlem $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> J ~<_ K ) ) $=
      ( vf vu vv ctop wcel wa wbr cima 3impia syl wceq imaeq2 cuni eqid elssuni
      cv wss f1imacnv chomeo chomeosm co wex cdom c0 wne hmph syl6bb simp1 wral
      n0 w3a wi hmeocnb ra4 ccnv wf1 wf1o hmeomap adantr f1of1 ad2antrl syl2anc
      wb ad2antll eqeq12d syl5ib impbid1 ex dom2d mpd 3expia exlimdv sylbid ) A
      FGZBFGZHZABUAIZCRZABUBUCZGZCUDZABUEIZVRVSWAUFUGWCABUHCWAULUIVRWBWDCVPVQWB
      WDVPVQWBUMZVPWDVPVQWBUJWEDEABVTDRZJZVTERZJZFWEWGBGZDAUKZWFAGZWJUNVPVQWBWK
      DVTABUOKWJDAUPLWEWLWHAGZHZWGWIMZWFWHMZVEWEWNHZWOWPWOVTUQZWGJZWRWIJZMWQWPW
      GWIWRNWQWSWFWTWHWQAOZBOZVTURZWFXASZWSWFMWQXAXBVTUSZXCWEXEWNVPVQWBXEVTABXA
      XBXAPXBPUTKVAXAXBVTVBLZWLXDWEWMWFAQVCXAXBWFVTTVDWQXCWHXASZWTWHMXFWMXGWEWL
      WHAQVFXAXBWHVTTVDVGVHWFWHVTNVIVJVKVLVMVNVO $.
      $( [31-May-2014] $)
  $}

  $( Homeomorphisms preserve the cardinality of the topologies.  (Contributed
     by FL, 1-Jun-2008.)  (The proof was shortened by Mario Carneiro,
     31-May-2014.) $)
  homcard $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> J ~~ K ) ) $=
    ( ctop wcel wa chomeo wbr cdom homcardlem hmphsyma wi ancoms syld jcad sbth
    cen syl6 ) ACDZBCDZEZABFGZABHGZBAHGZEABPGTUAUBUCABITUABAFGZUCABJSRUDUCKBAIL
    MNABOQ $.
    $( [31-May-2014] $) $( [1-Jun-2008] $)

  ${
    $d J f $.  $d K f $.  $d X f $.  $d Y f $.
    homcardus.1 $e |- X = U. J $.
    homcardus.2 $e |- Y = U. K $.
    $( Homeomorphisms preserve the cardinality of the underlying sets.
       (Contributed by FL, 17-Aug-2008.) $)
    homcardus $p |- ( ( J e. Top /\ K e. Top ) -> ( J ~= K -> X ~~ Y ) ) $=
      ( vf ctop wcel wa chomeo wbr cv chomeosm co wex cen c0 wne cvv n0 hmeomap
      hmph syl6bb wf1o vex f1oen2g mpan syl6 exlimdv sylbid ) AHIBHIJZABKLZGMZA
      BNOZIZGPZCDQLZULUMUORSUQABUCGUOUAUDULUPURGULUPCDUNUEZURUNABCDEFUBUNTIUSUR
      GUFCDTUNUGUHUIUJUK $.
      $( [31-May-2014] $) $( [17-Aug-2008] $)
  $}

  ${
    $d A f x $.  $d B f x $.
    eqindhome.1 $e |- A e. _V $.
    eqindhome.2 $e |- B e. _V $.
    $( Equinumerous sets equipped with their indiscrete topologies are
       homeomorph (which means in that particular case that a segment is
       homeomorph to a circle contrary to what Wikipedia claims).  (Contributed
       by FL, 17-Aug-2008.)  (The proof was shortened by Mario Carneiro,
       31-May-2014.) $)
    eqindhome $p |- ( A ~~ B -> { (/) , A } ~= { (/) , B } ) $=
      ( vf wf1o c0 cpr co wbr wcel ccn ctop wf indistop f1of cuni cun 0ex unipr
      cv wex chomeosm wne cen chomeo uncom un0 3eqtrri mapudiscn sylancr f1ocnv
      ccnv syl wa wb hmeobc mp2an sylanbrc ne0i exlimiv bren hmph 3imtr4i ) ABE
      UAZFZEUBGAHZGBHZUCIZGUDZABUEJVGVHUFJZVFVJEVFVEVIKZVJVFVEVGVHLIKZVEUMZVHVG
      LIKZVLVFVGMKZABVENVMAOZABVEPABVEVGVGQGARAGRAGASCTGAUGAUHUIDUJUKVFVHMKZBAV
      NNZVOBOZVFBAVNFVSABVEULBAVNPUNBAVNVHVHQGBRBGRBGBSDTGBUGBUHUICUJUKVPVRVLVM
      VOUOUPVQVTVEVGVHUQURUSVIVEUTUNVAABEDVBVPVRVKVJUPVQVTVGVHVCURVD $.
      $( [31-May-2014] $) $( [17-Aug-2008] $)
  $}

  $( ` { (/) } ` is the only topology with one element.  (Contributed by FL,
     18-Aug-2008.) $)
  top1 $p |- ( J e. Top -> ( J ~~ 1o <-> J = { (/) } ) ) $=
    ( ctop wcel c1o cen wbr c0 csn wceq wi 0opn en1eqsn ex syl 0ex ensn1 mpbiri
    breq1 impbid1 ) ABCZADEFZAGHZIZTGACZUAUCJAKUDUAUCGALMNUCUAUBDEFGOPAUBDERQS
    $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( If a topology has two element it is the indiscrete topology.  (Contributed
     by FL, 11-Aug-2008.) $)
  top2ind $p |- ( ( J e. Top /\ J ~~ 2o ) -> J = { (/) , U. J } ) $=
    ( ctop wcel c2o cen wbr c0 cuni cpr wceq csn wb wi 0top eqid topopn wn csdm
    c1o wne 0ex ensn1 breq1 mpbiri 1sdom2 wa ensdomtr sdomnen syl sylancl con2i
    0opn bibif df-ne necom 3exp com24 com12 sylbi sylbir syl6bi com3r mpd com4l
    set2elt syl3c imp ) ABCZADEFZAGAHZIJZVHVJGJZAGKZJZLZVJACZGACZVIVKMANAVJVJOP
    AULVIVOVPVQVKVIVNQZVOVPVQVKMMZMVNVIVNASEFZSDRFZVIQZVNVTVMSEFGUAUBAVMSEUCUDU
    EVTWAUFADRFWBASDUGADUHUIUJUKVRVOVIVSVRVOVLQZVIVSMZVLVNUMWCVJGTZWDVJGUNWEGVJ
    TZWDVJGUOVIWFVSVIVQVPWFVKVIVQVPWFVKMGVJAVEUPUQURUSUTVAVBVCVDVFVG $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( If a topology has two elements its underlying set can't be empty.
     (Contributed by FL, 11-Aug-2008.) $)
  top2usne $p |- ( ( J e. Top /\ J ~~ 2o ) -> U. J =/= (/) ) $=
    ( wcel c2o cen wbr wa c0 wceq wn wne wi wss breq1 csuc sssucid peano2 ax-mp
    com ensymg c1o ctop cuni cpr top2ind uni0b wo sssn sstr nsuceq0 necomi wpss
    csn df-pss peano1 php nsyl mpan sylbir mp2an df-2o df-1o suceq eqtri breq2i
    sylancl mtbir pm2.21i syl6bi a1i com13 adantl com12 0ex ensn1 2onn entr cvv
    1onn elexi php5 syl6 a1d syl ex eqcoms mpi sylbi mpcom pm2.01d df-ne sylibr
    jaoi ) AUABZACDEZFZAUBZGHZIZWPGJWOWQAGWPUCHZWOWQWRKAUDWQWOWSWRWQAGULZLZWOWS
    WRKZKZAUEXAAGHZAWTHZUFXCAGUGXDXCXEWOXDXBWNXDXBKWMWSXDWNWRXDWNWRKKWSXDWNGCDE
    ZWRAGCDMXFWRXFGGNZNZDEZGXGLZXGXHLZXIIZGOXGOXJXKFGXHLZGXHJZXLGXGXHUHXHGXGUIU
    JXMXNFGXHUKZXLGXHUMXHRBZXOXLXGRBZXPGRBXQUNGPQXGPQZXPXOFXHGDEZXIXHGUOXPXIXSK
    XRGXHRSQUPUQURVEUSCXHGDCTNZXHUTTXGHXTXHHVATXGVBQVCVDVFVGVHVIVJVKVLXEWTTDEZX
    CGVMVNYAXCKWTAWTAHYAATDEZXCWTATDMWOYBXBWNYBXBKZWMCRBZWNYCKVOYDWNCADEZYCACRS
    YEYBXBYEYBFCTDEZXBCATVPYFWRWSTVQBZYFWRKTRVRVSYGYFTCDEZWRCTVQSYHWRYHTXTDEZTR
    BYIIVRTVTQCXTTDUTVDVFVGWAQWBWCWDWAQVKVLVHWEWFWLWGWGVJWHWIWPGWJWK $.
    $( [1-Sep-2008] $) $( [11-Aug-2008] $)

  $( An unnamed topological preservation theorem.  (Contributed by FL,
     18-Aug-2008.) $)
  homindlem2 $p |- ( ( K e. Top /\ K ~= { (/) } ) -> K = { (/) } ) $=
    ( ctop wcel c0 csn chomeo wbr wa c1o cen wceq wi sn0top homcard mpan2 ensn1
    imp 0ex entr sylancl wb top1 adantr mpbid ) ABCZADEZFGZHZAIJGZAUFKZUHAUFJGZ
    UFIJGUIUEUGUKUEUFBCUGUKLMAUFNOQDRPAUFISTUEUIUJUAUGAUBUCUD $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)

  $( Homeomorphisms preserve topological indiscretion.  (Contributed by FL,
     18-Aug-2008.) $)
  homindlem3 $p |- ( ( K e. Top /\ A =/= (/) /\ A e. B ) -> ( { (/) , A } ~= K
     -> K = { (/) , U. K } ) ) $=
    ( ctop wcel c0 wne w3a cpr chomeo wbr c2o cen wa cuni wceq simpl1 wi cvv ex
    elex cif preq2 eleq1d indistop dedth hmphsyma homcard ancoms syld com12 a1d
    3syl 3imp1 0ex necom pr2nelem 3exp com13 sylbi mpii a1i 3imp adantr syl2anc
    entr jca top2ind syl6 ) CDEZAFGZABEZHZFAIZCJKZVJCLMKZNZCFCOIPVMVOVQVMVONZVJ
    VPVJVKVLVOQVRCVNMKZVNLMKZVPVJVKVLVOVSVJVLVOVSRZRVKVLVJWAVLASEZVNDEZVJWARABU
    AWBWCFWBAFUBZIZDEAFAWDPVNWEDAWDFUCUDWDUEUFWCVJWAWCVJNVOCVNJKZVSVNCUGVJWCWFV
    SRCVNUHUIUJTUMUKULUNVMVTVOVJVKVLVTVKVLVTRRVJVKVLFSEZVTUOVKFAGZVLWGVTRRAFUPW
    GVLWHVTWGVLWHVTFASBUQURUSUTVAVBVCVDCVNLVFVEVGTCVHVI $.
    $( [1-Sep-2008] $) $( [18-Aug-2008] $)


