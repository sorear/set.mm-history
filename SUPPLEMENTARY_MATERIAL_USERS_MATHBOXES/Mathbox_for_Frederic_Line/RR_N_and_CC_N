$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        ( RR ^ N ) and ( CC ^ N )
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +cv $. $( Addition of complex vectors. $)

  $( Extends class notation with addition of complex vectors. $)
  cplcv $a class +cv $.

  ${
    $d n u v x $.
    $( Addition of complex vectors in a space of dimension ` n ` .
       Experimental. $)
    df-addcv $a |- +cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( ( u ` x ) + ( v ` x ) ) ) ) ) $.
  $}

  ${
    isaddrv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x v u U $.  $d x v u n N $.  $d x u v V $.
      $( Addition of complex vectors.  Experimental.  (Contributed by FL,
         14-Sep-2013.) $)
      isaddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
      V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) =
      ( x e. ( 1 ... N ) |-> ( ( U ` x ) + ( V ` x ) ) ) ) $=
        ( vu vv vn wcel cc c1 cfz co cmap cv cfv caddc cmpt wceq cn cmpt2 cplcv
        w3a oveq2 oveq2d eqidd mpteq12dv mpt2eq123dv df-addcv ovex mpt2ex fvmpt
        syl5eq 3ad2ant1 oveqd fveq1 oveq1d mpteq2dv mptex ovmpt2 3adant1 eqtrd
        eqid ) CUAJZBKLCMNZONZJZDVGJZUDZBDENBDGHVGVGAVFAPZGPZQZVKHPZQZRNZSZUBZN
        ZAVFVKBQZVKDQZRNZSZVJEVRBDVEVHEVRTVIVEECUCQVRFICGHKLIPZMNZONZWFAWEVPSZU
        BVRUAUCWDCTZGHWFWFWGVGVGVQWHWEVFKOWDCLMUEZUFZWJWHAWEVPVFVPWIWHVPUGUHUIA
        HGIUJGHVGVGVQKVFOUKZWKULUMUNUOUPVHVIVSWCTVEGHBDVGVGVQWCVRAVFVTVORNZSVLB
        TZAVFVPWLWMVMVTVORVKVLBUQURUSVNDTZAVFWLWBWNVOWAVTRVKVNDUQUFUSVRVDAVFWBL
        CMUKUTVAVBVC $.
        $( [14-Sep-2013] $)
    $}

    ${
      $d x U $.  $d x N $.  $d x V $.
      $( Closure of addition of complex vectors.  (Contributed by FL,
         14-Sep-2013.) $)
      claddrv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
        V e. ( CC ^m ( 1 ... N ) ) ) -> ( U +w V ) e. ( CC ^m ( 1 ... N ) ) )
          $=
        ( vx wcel cc c1 cfz co cfv wf wa wi cnex elmap ffvelrn ex cvv cn w3a cv
        cmap caddc cmpt isaddrv ovex sylbi 3ad2ant2 3ad2ant3 addcl syl2anc eqid
        imp fmptd wb pm3.2i a1i elmapg syl mpbird eqeltrd ) BUAGZAHIBJKZUDKZGZC
        VFGZUBZACDKFVEFUCZALZVJCLZUEKZUFZVFFABCDEUGVIVNVFGZVEHVNMZVIFVEVMHVNVIV
        JVEGZNVKHGZVLHGZVMHGVIVQVRVGVDVQVROZVHVGVEHAMZVTHVEAPIBJUHZQWAVQVRVEHVJ
        ARSUIUJUOVIVQVSVHVDVQVSOZVGVHVEHCMZWCHVECPWBQWDVQVSVEHVJCRSUIUKUOVKVLUL
        UMVNUNUPVIHTGZVETGZNZVOVPUQWGVIWEWFPWBURUSHVEVNTTUTVAVBVC $.
        $( [14-Sep-2013] $)
    $}

    ${
      $d x N $.  $d x U $.  $d x V $.
      $( Closure of addition of real vectors.  (Contributed by FL,
         29-May-2014.) $)
      claddrvr $p |- ( ( N e. NN /\ U e. ( RR ^m ( 1 ... N ) ) /\
        V e. ( RR ^m ( 1 ... N ) ) ) -> ( U +w V ) e. ( RR ^m ( 1 ... N ) ) )
          $=
        ( vx wcel cr c1 cfz co cmap cfv cc wf ax-resscn reex elmap wa wi cn w3a
        caddc cmpt wceq simp1 wss fss mpan2 ovex cnex 3imtr4i 3ad2ant2 3ad2ant3
        cv isaddrv syl3anc wral ffvelrn im2anan9 readdcl ancoms syl6 exp3acom3r
        pm2.43i sylbi com12 imp ralrimiv 3adant1 eqid fmpt sylib sylibr eqeltrd
        ex ) BUAGZAHIBJKZLKZGZCVSGZUBZACDKZFVRFUOZAMZWDCMZUCKZUDZVSWBVQANVRLKZG
        ZCWIGZWCWHUEVQVTWAUFVTVQWJWAVRHAOZVRNAOZVTWJWLHNUGZWMPVRHNAUHUIHVRAQIBJ
        UJZRZNVRAUKWORULUMWAVQWKVTVRHCOZVRNCOZWAWKWQWNWRPVRHNCUHUIHVRCQWORZNVRC
        UKWORULUNFABCDEUPUQWBVRHWHOZWHVSGWBWGHGZFVRURZWTVTWAXBVQVTWASXAFVRVTWAW
        DVRGZXATZVTWLWAXDTWPWAWLXDWAWQWLXDTWSXCWQWLXAXCWQWLSZXATXEXCXCXAXEXCXCS
        WFHGZWEHGZSXAWQXCXFWLXCXGWQXCXFVRHWDCUSVPWLXCXGVRHWDAUSVPUTXGXFXAWEWFVA
        VBVCVDVEVDVFVGVFVHVIVJFVRHWGWHWHVKVLVMHVRWHQWORVNVO $.
        $( [29-May-2014] $)
    $}

  $}

  ${
    $d n u v x N $.
    sigadd.1 $e |- +w = ( +cv ` N ) $.
    $( Functionality of vector addition.  (Contributed by FL, 29-May-2014.) $)
    sigadd $p |- ( N e. NN -> +w :
      ( ( CC ^m ( 1 ... N ) ) X. ( CC ^m ( 1 ... N ) ) )
          --> ( CC ^m ( 1 ... N ) ) ) $=
      ( vu vv vx vn wcel cc c1 cfz co cmap wf cv cfv wral wa cvv wceq cxp caddc
      cn cmpt cmpt2 wi cnex ovex elmap ffvelrn addcl syl2an an42s ex exp3acom3r
      pm2.43i com12 ralrimiv a1d syl2anb impcom eqid fmpt wb a1i elmapg sylancr
      sylib mpbird ralrimivva fmpt2 cplcv pm3.2i mpt2exga syl oveq2 mpt2eq123dv
      oveq2d mpteq1 df-addcv fvmptg mpdan syl5eq feq1d ) AUCHZIJAKLZMLZWGUAZWGB
      NWHWGDEWGWGFWFFOZDOZPZWIEOZPZUBLZUDZUEZNZWEWOWGHZEWGQDWGQWQWEWRDEWGWGWEWJ
      WGHZWLWGHZRZRZWRWFIWONZXBWNIHZFWFQZXCXAWEXEWSWFIWJNZWFIWLNZWEXEUFWTIWFWJU
      GJAKUHZUIIWFWLUGXHUIXFXGRZXEWEXIXDFWFWIWFHZXIXDXJXIXDUFXIXJXJXDXIXJXJRXDX
      FXJXGXJXDXFXJRWKIHWMIHXDXGXJRWFIWIWJUJWFIWIWLUJWKWMUKULUMUNUOUPUQURUSUTVA
      FWFIWNWOWOVBVCVHXBISHWFSHZWRXCVDUGXKXBXHVEIWFWOSSVFVGVIVJDEWGWGWOWGWPWPVB
      VKVHWEWHWGBWPWEBAVLPZWPCWEWPSHZXLWPTWEWGSHZXNRZXMXOWEXNXNIWFMUHZXPVMVEDEW
      GWGWOSSVNVOGADEIJGOZKLZMLZXSFXRWNUDZUEWPUCSVLXQATZDEXSXSXTWGWGWOYAXRWFIMX
      QAJKVPZVRZYCYAXRWFTXTWOTYBFXRWFWNVSVOVQFEDGVTWAWBWCWDVI $.
      $( [29-May-2014] $)
  $}

  $c 0cv $.

  $( Extends class notation with null vector. $)
  c0cv $a class 0cv $.

  ${
    $d n x $.
    $( The null vector in a space of dimension ` n ` .  Experimental. $)
    df-nullcv $a |- 0cv = ( n e. NN |-> ( x e. ( 1 ... n ) |-> 0 ) ) $.
  $}

  ${
    isnullcv.1 $e |- 0w = ( 0cv ` N ) $.
    ${
      $( The null vector. $)
      $d x n N $.
      $( The null vector in a space of dimension ` n ` .  (Contributed by FL,
         15-Sep-2013.) $)
      isnullcv $p |- ( N e. NN -> 0w = ( x e. ( 1 ... N ) |-> 0 ) ) $=
        ( vn cn wcel c1 cfz co cc0 cmpt cvv wceq ovex mptex wa c0cv cfv cv
        oveq2 eqidd mpteq12dv df-nullcv fvmptg syl5eq mpan2 ) BFGZAHBIJZKLZMGZC
        UJNAUIKHBIOPUHUKQCBRSUJDEBAHETZIJZKLUJFMRULBNZAUMKUIKULBHIUAUNKUBUCAEUD
        UEUFUG $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x N $.
      $( The null vector is a complex vector.  (Contributed by FL,
         15-Sep-2013.) $)
      zernpl $p |- ( N e. NN -> 0w e. ( CC ^m ( 1 ... N ) ) ) $=
        ( vx cn wcel c1 cfz co cc0 cmpt cc cmap isnullcv wf cv wa 0cn a1i eqid
        fmptd cnex ovex elmap sylibr eqeltrd ) AEFZBDGAHIZJKZLUHMIZDABCNUGUHLUI
        OUIUJFUGDUHJLUIJLFUGDPUHFQRSUITUALUHUIUBGAHUCUDUEUF $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x A $.  $d x n N $.
      $( Value of the complex vector at a specific coordinate.  (Contributed by
         FL, 15-Sep-2013.) $)
      valvze $p |- ( ( N e. NN /\ A e. ( 1 ... N ) ) -> ( 0w ` A ) = 0 ) $=
        ( vn vx cn wcel c1 cfz co cfv c0cv cc0 cv cmpt wceq a1i fveq1d cvv ovex
        wa fveq1i df-nullcv mptexg syl oveq2 eqidd mpteq12dv eqid fvmptg adantr
        mpdan cc simpr 0cn sylancl 3eqtrd syl5eq ) BGHZAIBJKZHZUBZACLABMLZLZNAC
        VDDUCVCVEABEGFIEOZJKZNPZPZLZLAFVANPZLZNVCAVDVJVCBMVIMVIQVCFEUDRSSVCAVJV
        KUTVJVKQZVBUTVKTHZVMUTVATHZVNVOUTIBJUARFVANTUEUFEBVHVKGTVIVFBQZFVGNVANV
        FBIJUGVPNUHUIVIUJUKUMULSVCVBNUNHVLNQUTVBUOUPFANNVAUNVKFOAQNUHVKUJUKUQUR
        US $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    addcomv.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x A $.  $d x B $.  $d x N $.
      $( Vector addition is commutative.  (Contributed by FL, 15-Sep-2013.) $)
      addcomv $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w B ) = ( B +w A ) ) $=
        ( vx wcel cc c1 cfz co w3a cfv caddc cmpt wa wceq wi wf cnex cn cmap cv
        ovex elmap ffvelrn 3adant1 3adant2 jca sylbi com12 imp addcom mpteq2dva
        3exp syl isaddrv 3com23 3eqtr4d ) CUAGZAHICJKZUBKZGZBVBGZLZFVAFUCZAMZVF
        BMZNKZOFVAVHVGNKZOZABDKBADKZVEFVAVIVJVEVFVAGZPVGHGZVHHGZPZVIVJQVEVMVPVC
        VDVMVPRZUTVCVDVQVCVAHASZVDVQRHVAATICJUDZUEVDVRVQVDVAHBSZVRVQRHVABTVSUEV
        TVRVMVPVTVRVMLVNVOVRVMVNVTVAHVFAUFUGVTVMVOVRVAHVFBUFUHUIUOUJUKUJULUGULV
        GVHUMUPUNFACBDEUQUTVDVCVLVKQFBCADEUQURUS $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x y A $.  $d x y B $.  $d x y C $.  $d x y N $.
      $( Vector addition is associative.  (Contributed by FL, 15-Sep-2013.) $)
      addassv $p |- ( ( N e. NN /\ ( A e. ( CC ^m ( 1 ... N ) ) /\
        B e. ( CC ^m ( 1 ... N ) ) /\ C e. ( CC ^m ( 1 ... N ) ) ) ) ->
        ( ( A +w B ) +w C ) = ( A +w ( B +w C ) ) ) $=
        ( vx vy wcel cc co wa cfv caddc wceq cvv wi cnex ffvelrnb mp2an cn cmap
        c1 cfz w3a cv cmpt isaddrv 3adant3r3 oveq1d simpl wf simpr1 ovex simpr2
        sylan addcl syl2anc eqid wb pm3.2i a1i elmapg syl mpbird simpr3 syl3anc
        fmptd 3adant3r1 oveq2d ex 3ad2ant1 adantl 3ad2ant2 3ad2ant3 simpr fveq2
        imp addass oveq12d fvmptg sylancl 3eqtr4d mpteq2dva 3eqtr4rd 3eqtrd ) D
        UAIZAJUCDUDKZUBKZIZBWIIZCWIIZUEZLZABEKZCEKGWHGUFZAMZWPBMZNKZUGZCEKZHWHH
        UFZWTMZXBCMZNKZUGZABCEKZEKZWNWOWTCEWGWJWKWOWTOWLGADBEFUHUIUJWNWGWTWIIZW
        LXAXFOWGWMUKZWNXIWHJWTULZWNGWHWSJWTWNWPWHIZLZWQJIZWRJIZWSJIWNWJXLXNWGWJ
        WKWLUMZWHPIZJPIZWJXLLXNQUCDUDUNZRWHJWPPPASTUPWNWKXLXOWGWJWKWLUOXQXRWKXL
        LXOQXSRWHJWPPPBSTUPZWQWRUQURWTUSZVHWNXRXQLZXIXKUTYBWNXRXQRXSVAVBZJWHWTP
        PVCVDVEWGWJWKWLVFZHWTDCEFUHVGWNAGWHWRWPCMZNKZUGZEKZHWHXBAMZXBYGMZNKZUGZ
        XHXFWNWGWJYGWIIZYHYLOXJXPWNYMWHJYGULZWNGWHYFJYGXMXOYEJIZYFJIXTWNWLXLYOY
        DXQXRWLXLLYOQXSRWHJWPPPCSTUPWRYEUQURYGUSZVHWNYBYMYNUTYCJWHYGPPVCVDVEHAD
        YGEFUHVGWNXGYGAEWGWKWLXGYGOWJGBDCEFUHVIVJWNHWHXEYKWNXBWHIZLZYIXBBMZNKZX
        DNKZYIYSXDNKZNKZXEYKYRYIJIZYSJIZXDJIZUUAUUCOWNYQUUDWMYQUUDQZWGWJWKUUGWL
        WJYQUUDXQXRWJYQLUUDQXSRWHJXBPPASTVKVLVMVRWNYQUUEWMYQUUEQZWGWKWJUUHWLWKY
        QUUEXQXRWKYQLUUEQXSRWHJXBPPBSTVKVNVMVRWNYQUUFWMYQUUFQZWGWLWJUUIWKWLYQUU
        FXQXRWLYQLUUFQXSRWHJXBPPCSTVKVOVMVRYIYSXDVSVGYRYQYTPIZXEUUAOWNYQVPZYIYS
        NUNYQUUJLXCYTXDNGXBWSYTWHPWTWPXBOZWQYIWRYSNWPXBAVQWPXBBVQZVTYAWAUJWBYRY
        QUUBPIZYKUUCOUUKYSXDNUNYQUUNLYJUUBYINGXBYFUUBWHPYGUULWRYSYEXDNUUMWPXBCV
        QVTYPWAVJWBWCWDWEWF $.
        $( [15-Sep-2013] $)
    $}
  $}

  ${
    $d x A a $.  $d x N $.  $d x 0w $.
    addidv2.1 $e |- 0w = ( 0cv ` N ) $.
    addidv2.2 $e |- +w = ( +cv ` N ) $.
    $( The null vector is a left identity for vector addition.  (Contributed by
       FL, 15-Sep-2013.) $)
    addidv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( 0w +w A ) = A ) $=
      ( vx cn wcel cc c1 cfz co wa cfv caddc cmpt cc0 wceq wi cv valvze adantlr
      cmap oveq1d wf cnex ovex elmap ffvelrn ex a1i syl5bi addid2 syl mpteq2dva
      imp31 eqtrd simpl zernpl adantr simpr isaddrv syl3anc feqmptd 3eqtr4d
      sylib ) BHIZAJKBLMZUDMZIZNZGVIGUAZDOZVMAOZPMZQZGVIVOQDACMZAVLGVIVPVOVLVMV
      IIZNZVPRVOPMZVOVTVNRVOPVHVSVNRSVKVMBDEUBUCUEVTVOJIZWAVOSVHVKVSWBVKVIJAUFZ
      VHVSWBTZJVIAUGKBLUHUIZWCWDTVHWCVSWBVIJVMAUJUKULUMUQVOUNUOURUPVLVHDVJIZVKV
      RVQSVHVKUSVHWFVKBDEUTVAVHVKVBZGDBACFVCVDVLGVIJAVLVKWCWGWEVGVEVF $.
      $( [15-Sep-2013] $)

    $( The null vector is a right identity for vector addition.  (Contributed
       by FL, 15-Sep-2013.) $)
    addidrv2 $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) )
      -> ( A +w 0w ) = A ) $=
      ( cn wcel cc c1 cfz co cmap wa wceq zernpl adantr addcomv mpd3an3 addidv2
      eqtrd ) BGHZAIJBKLMLZHZNADCLZDACLZAUBUDDUCHZUEUFOUBUGUDBDEPQADBCFRSABCDEF
      TUA $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d u v +w $.  $d u v N $.
    vecaddonto.1 $e |- +w = ( +cv ` N ) $.
    $( Vector addition is onto.  (Contributed by FL, 29-May-2014.) $)
    vecaddonto $p |- ( N e. NN ->
     +w : ( ( CC ^m ( 1 ... N ) ) X. ( CC ^m ( 1 ... N ) ) )
       -onto-> ( CC ^m ( 1 ... N ) ) ) $=
      ( vu vv cn wcel cc c1 cfz co cmap cxp wf cv cfv wceq wrex wral sylanbrc
      wfo sigadd wa c0cv cop eqid zernpl adantr simpr vex opelxp df-ov syl5reqr
      addidv2 fveq2 eqeq2d rcla4ev syl2anc ralrimiva dffo3 ) AFGZHIAJKLKZVBMZVB
      BNDOZEOZBPZQZEVCRZDVBSVCVBBUAABCUBVAVHDVBVAVDVBGZUCZAUDPZVDUEZVCGZVDVLBPZ
      QZVHVJVKVBGZVIVMVAVPVIAVKVKUFZUGUHVAVIUIVKVDVBVBDUJUKTVJVNVKVDBKVDVKVDBUL
      VDABVKVQCUNUMVGVOEVLVCVEVLQVFVNVDVEVLBUOUPUQURUSEDVCVBBUTT $.
      $( [29-May-2014] $)
  $}

  ${
    cnegvex2.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2.2 $e |- 0w = ( 0cv ` N ) $.
    cnegvex2.3 $e |- N e. NN $.
    ${
      $d x y A i $.  $d x y i N $.
      $( Existence of a left inverse for vector addition.  (Contributed by FL,
         15-Sep-2013.) $)
      cnegvex2 $p |- ( A e. ( CC ^m ( 1 ... N ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
        ( vi vy cc c1 co wcel wceq cfv caddc cmpt cc0 cmul cfz cmap cv ci wf wa
        wrex ax-icn mulcli cnex elmap ffvelrn sylanb mulcl sylancr fmptd sylibr
        ovex ax-1cn adddir mp3an12 ax-i2m1 oveq1i syl5eq mulid2 oveq2d 3eqtr3rd
        eqid mul02 syl mpteq2dva ax-17 hbmpt1 hbeq cvv fveq1 fvmpt2 eqtr expcom
        wi com3l mpsyl imp oveq1d mpteq2da eqeq1d rcla4ev syl2anc cplcv isaddrv
        ex cn mp3an1 ancoms oveq syl5ibr ax-mp c0cv isnullcv eqtri a1i rexbidva
        eqeq12d mpbird ) BKLCUAMZUBMZNZAUCZBDMZEOZAXFUGIXEIUCZXHPZXKBPZQMZRZIXE
        SRZOZAXFUGZXGIXEUDUDTMZXMTMZRZXFNZIXEXTXMQMZRZXPOZXRXGXEKYAUEYBXGIXEXTK
        YAXGXKXENZUFZXSKNZXMKNZXTKNUDUDUHUHUIZXGXEKBUEYFYIKXEBUJLCUAURZUKXEKXKB
        ULUMZXSXMUNUOYAVHZUPKXEYAUJYKUKUQXGIXEYCSYGYIYCSOYLYIXSLQMZXMTMZXTLXMTM
        ZQMZSYCYHLKNYIYOYQOYJUSXSLXMUTVAYIYOSXMTMSYNSXMTVBVCXMVIVDYIYPXMXTQXMVE
        VFVGVJVKXQYEAYAXFXHYAOZXOYDXPYRIXEXNYCIJJXHYAJUCXHNIVLIJXEXTVMVNYRYFUFX
        LXTXMQYRYFXLXTOZXTVONZYRXLXKYAPZOZYFYSVTXSXMTURXKXHYAVPYFYTUUBYSYFYTUUB
        YSVTZYFYTUFUUAXTOZUUCIXEXTVOYAYMVQUUBUUDYSXLUUAXTVRVSVJWKWAWBWCWDWEWFWG
        WHXGXJXQAXFXGXHXFNZUFZXIXOEXPDCWIPZOZUUFXIXOOZVTFUUFUUIUUHXHBUUGMZXOOZU
        UEXGUUKCWLNZUUEXGUUKHIXHCBUUGUUGVHWJWMWNUUHXIUUJXOXHBDUUGWOWFWPWQEXPOUU
        FECWRPZXPGUULUUMXPOHICUUMUUMVHWSWQWTXAXCXBXD $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d i x y A $.  $d i x y N $.
      $( Existence of a left inverse for vector addition.  (Contributed by FL,
         29-May-2014.) $)
      rnegvex2 $p |- ( A e. ( RR ^m ( 1 ... N ) ) ->
       E. x e. ( RR ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
        ( vi vy cr co wcel wceq cfv caddc wf wa elmap cc cfz cmap wrex cmpt cc0
        c1 cv cneg ffvelrn renegcl syl eqid fmptd reex ovex 3imtr4i recnd negcl
        sylanb addcom syl2anc negid eqtrd mpteq2dva wel ax-17 hbmpt1 hbeq negex
        cvv wi fveq1 fvmpt2 eqtr expcom com3l mpsyl imp oveq1d mpteq2da rcla4ev
        ex eqeq1d cplcv cn id wss ax-resscn fss mpan2 cnex syl3an mp3an1 ancoms
        isaddrv oveq syl5ibr ax-mp isnullcv a1i eqeq12d rexbidva mpbird ) BKUFC
        UALZUBLZMZAUGZBDLZENZAXEUCIXDIUGZXGOZXJBOZPLZUDZIXDUEUDZNZAXEUCZXFIXDXL
        UHZUDZXEMZIXDXRXLPLZUDZXONZXQXDKBQZXDKXSQXFXTYDIXDXRKXSYDXJXDMZRZXLKMXR
        KMXDKXJBUIZXLUJUKXSULZUMKXDBUNUFCUAUOZSZKXDXSUNYISUPXFIXDYAUEXFYERZYAXL
        XRPLZUEYKXRTMZXLTMZYAYLNYKYNYMXFYDYEYNYJYFXLYGUQUSZXLURUKYOXRXLUTVAYKYN
        YLUENYOXLVBUKVCVDXPYCAXSXEXGXSNZXNYBXOYPIXDXMYAIJJXGXSJAVEIVFIJXDXRVGVH
        YPYERXKXRXLPYPYEXKXRNZXRVJMZYPXKXJXSOZNZYEYQVKXLVIXJXGXSVLYEYRYTYQYEYRY
        TYQVKZYEYRRYSXRNZUUAIXDXRVJXSYHVMYTUUBYQXKYSXRVNVOUKWBVPVQVRVSVTWCWAVAX
        FXIXPAXEXFXGXEMZRZXHXNEXODCWDOZNZUUDXHXNNZVKFUUDUUGUUFXGBUUELZXNNZUUCXF
        UUICWEMZUUCXFUUIHUUJUUJUUCXGTXDUBLZMZXFBUUKMZUUIUUJWFXDKXGQZXDTXGQZUUCU
        ULUUNKTWGZUUOWHXDKTXGWIWJKXDXGUNYISTXDXGWKYISUPYDXDTBQZXFUUMYDUUPUUQWHX
        DKTBWIWJYJTXDBWKYISUPIXGCBUUEUUEULWOWLWMWNUUFXHUUHXNXGBDUUEWPWCWQWREXON
        ZUUDUUJUURHICEGWSWRWTXAXBXC $.
        $( [29-May-2014] $)
    $}
  $}

  ${
    $d x A $.  $d x N $.
    cnegvex2b.1 $e |- +w = ( +cv ` N ) $.
    cnegvex2b.2 $e |- 0w = ( 0cv ` N ) $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    cnegvex2b $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) ) ->
       E. x e. ( CC ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( cn wcel cc c1 cfz co cmap cplcv cfv c0cv wceq wrex wi cif oveq2d eleq2d
      wa cv oveq2 fveq2 oveqd eqeq12d rexeqbidv imbi12d eqid 1nn cnegvex2 dedth
      elimel imp oveqi eqeq12i rexbii sylibr ) CHIZBJKCLMZNMZIZUDAUEZBCOPZMZCQP
      ZRZAVDSZVFBDMZERZAVDSVBVEVKVBVEVKTBJKVBCKUAZLMZNMZIZVFBVNOPZMZVNQPZRZAVPS
      ZTCKCVNRZVEVQVKWBWCVDVPBWCVCVOJNCVNKLUFUBZUCWCVJWAAVDVPWDWCVHVSVIVTWCVGVR
      VFBCVNOUGUHCVNQUGUIUJUKABVNVRVTVRULVTULCKHUMUPUNUOUQVMVJAVDVLVHEVIDVGVFBF
      URGUSUTVA $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x N $.
    rnegvex2b.1 $e |- +w = ( +cv ` N ) $.
    rnegvex2b.2 $e |- 0w = ( 0cv ` N ) $.
    $( Existence of a left inverse for vector addition.  (Contributed by FL,
       29-May-2014.) $)
    rnegvex2b $p |- ( ( N e. NN /\ A e. ( RR ^m ( 1 ... N ) ) ) ->
       E. x e. ( RR ^m ( 1 ... N ) ) ( x +w A ) = 0w ) $=
      ( cn wcel cr c1 cfz co cmap cplcv cfv c0cv wceq wrex wi cif oveq2d eleq2d
      wa cv oveq2 fveq2 oveqd eqeq12d rexeqbidv imbi12d eqid 1nn rnegvex2 dedth
      elimel imp oveqi eqeq12i rexbii sylibr ) CHIZBJKCLMZNMZIZUDAUEZBCOPZMZCQP
      ZRZAVDSZVFBDMZERZAVDSVBVEVKVBVEVKTBJKVBCKUAZLMZNMZIZVFBVNOPZMZVNQPZRZAVPS
      ZTCKCVNRZVEVQVKWBWCVDVPBWCVCVOJNCVNKLUFUBZUCWCVJWAAVDVPWDWCVHVSVIVTWCVGVR
      VFBCVNOUGUHCVNQUGUIUJUKABVNVRVTVRULVTULCKHUMUPUNUOUQVMVJAVDVLVHEVIDVGVFBF
      URGUSUTVA $.
      $( [29-May-2014] $)
  $}

  $c -cv $. $( Substraction of complex vectors. $)

  $( Extends class notation with substraction of complex vectors. $)
  cmcv $a class -cv $.

  ${
    $d n u v w $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       Experimental. $)
    df-subcv $a |- -cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        v e. ( CC ^m ( 1 ... n ) ) |->
        ( iota_ w e. ( CC ^m ( 1 ... n ) ) ( v ( +cv ` n ) w ) = u ) ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.  $d x +w $.
    addcanri.1 $e |- +w = ( +cv ` N ) $.
    addcanri.2 $e |- N e. NN $.
    addcanri.3 $e |- A e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.4 $e |- B e. ( CC ^m ( 1 ... N ) ) $.
    addcanri.5 $e |- C e. ( CC ^m ( 1 ... N ) ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanri $p |- ( ( A +w B ) = ( A +w C ) <-> B = C ) $=
      ( vx co wceq wcel wi w3a oveq2 3jca 3adant3 addassv c0cv cfv cc c1 cfz cv
      cmap wrex eqid cnegvex2 3ad2ant3 cn wa simpr simpl3 simpl2 sylancr simpl1
      3eqtr4d 3exp mp3an wb oveq1 eqeq12d eqcoms imbi2d syl5ibrcom mp2b addidv2
      rexlimiv mp2an 3eqtr3g impbii ) ABELZACELZMZBCMVPDUAUBZBELZVQCELZBCAUCUDD
      UELUGLZNZKUFZAELZVQMZKVTUHVPVRVSMZOZHKADEVQFVQUIZGUJWDWFKVTWBVTNZWFWDVPWC
      BELZWCCELZMZOZCVTNZBVTNZWAWHWLOJIHWMWNWAPZWHVPWKWOWHVPPZWBVNELZWBVOELZWIW
      JVPWOWQWRMWHVNVOWBEQUKWPDULNZWHWAWNPZWIWQMGWOWHWTVPWOWHUMZWHWAWNWOWHUNZWM
      WNWAWHUOZWMWNWAWHUPRSWBABDEFTUQWPWSWHWAWMPZWJWRMGWOWHXDVPXAWHWAWMXBXCWMWN
      WAWHURRSWBACDEFTUQUSUTVAWDWEWKVPWEWKVBVQWCVQWCMVRWIVSWJVQWCBEVCVQWCCEVCVD
      VEVFVGVJVHWSWNVRBMGIBDEVQWGFVIVKWSWMVSCMGJCDEVQWGFVIVKVLBCAEQVM $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x N $.
    addcanrg.1 $e |- +w = ( +cv ` N ) $.
    $( Cancellation law for vector addition.  (Contributed by FL,
       15-Sep-2013.) $)
    addcanrg $p |- ( ( N e. NN /\
      ( A e. ( CC ^m ( 1 ... N ) ) /\ B e. ( CC ^m ( 1 ... N ) ) /\
        C e. ( CC ^m ( 1 ... N ) ) ) )
     -> ( ( A +w B ) = ( A +w C ) <-> B = C ) ) $=
      ( vx wcel co w3a wa wceq cfv oveqi wi jca eqid simpll syl ex cn cc c1 cfz
      cmap cplcv eqeq12i c0cv cv wrex simpl simpr1 adantr cnegvex2b oveq eqeq1d
      oveq2 simprr simplr1 simplr2 addassv eqcomd simpr simplr3 3jca eqtr oveq1
      syl13anc eqeq12d biimpcd eqcoms expcom com3l com4l com13 imp impcom com3r
      3syl com12 syl6bi rexlimiv pm2.43i addidv2 syl2anc 3eqtr3d syl5bi impbid1
      mpd ax-mp ) DUAHZAUBUCDUDIUEIZHZBWLHZCWLHZJZKZABEIZACEIZLZBCLZWTABDUFMZIZ
      ACXBIZLZWQXAWRXCWSXDEXBABFNEXBACFNUGWQXEXAWQXEKZDUHMZBXBIZXGCXBIZBCXFXHXI
      LZXFWKWMKZGUIZAEIZXGLZGWLUJXFXJOZWQXKXEWQWKWMWKWPUKWKWMWNWOULPUMGADEXGFXG
      QZUNXNXOGWLXNXLWLHZXOEXBLZXNXQXOOZOFXRXNXLAXBIZXGLZXSXRXMXTXGXLAEXBUOUPYA
      XQXOXFYAXQKZXJXEWQYBXJOZXEXLXCXBIZXLXDXBIZLZWQYCOXCXDXLXBUQWQYBYFXJWQYBYF
      XJOZWQYBKZYDXTBXBIZLZYGYHWKXQWMWNYJWKWPYBRWQYAXQURWMWNWOWKYBUSWMWNWOWKYBU
      TWKXQWMWNJKYIYDXLABDXBXBQZVAVBVHYBWQYJYGOZYAXQWQYLOWQXQYAYLWQXQYAYLOZWQXQ
      KZWKXQWMWOJZKXTCXBIZYELZYMYNWKYOWKWPXQRYNXQWMWOWQXQVCWMWNWOWKXQUSWMWNWOWK
      XQVDVEPXLACDXBYKVAYFYQYAYJXJYQYAYJXJOOZOYEYDYQYEYDLZYRYQYSKYPYDLZYRYPYEYD
      VFYJYTYAXJYTYJYAXJOZYTYJKYPYILUUAYPYDYIVFUUAYIYPYAYIYPLXJYAYIXHYPXIXTXGBX
      BVGXTXGCXBVGVIVJVKSVLVMSVLVKVNVSTVOVPVQWITVRSVQVTTWAWJVTWBVSWCXFWKWNXHBLW
      KWPXERZWMWNWOWKXEUTBDXBXGXPYKWDWEXFWKWOXICLUUBWMWNWOWKXEVDCDXBXGXPYKWDWEW
      FTWGBCAEUQWH $.
      $( [15-Sep-2013] $)
  $}

  ${

    negveud.1 $e |- +w = ( +cv ` N ) $.
    ${
      $d x y A $.  $d x y B $.  $d x y N $.  $d x y +w $.
      $( Existential uniqueness of vector negatives.  (Contributed by FL,
         15-Sep-2013.) $)
      negveud $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) )
      /\ B e. ( CC ^m ( 1 ... N ) ) )
          -> E! x e. ( CC ^m ( 1 ... N ) ) ( A +w x ) = B ) $=
        ( vy wcel co w3a cv wceq wrex wa wi wral 3adant2 impcom eqtr ex cn cmap
        cc c1 cfz wreu c0cv cfv 3simpa eqid cnegvex2b claddrv com23 imp addcomv
        3exp 3expia 3adant3 addassv 3exp2 com34 3imp addidv2 3ad2ant3 oveq1 syl
        eqcomd expcom 3syl com4l 3ad2ant2 mp2d mpd oveq2 eqeq1d rcla4ev syl2anc
        rexlimiv simpl1 simpl2 simprl simprr addcanrg biimpd com3r eqcoms com3l
        pm2.43i syl13anc ralrimivv reu4 sylanbrc ) DUAHZBUCUDDUEIUBIZHZCWNHZJZB
        AKZEIZCLZAWNMZWTBGKZEIZCLZNZWRXBLZOZGWNPAWNPWTAWNUFWQXAWQWMWONXBBEIZDUG
        UHZLZGWNMWQXAOZWMWOWPUIGBDEXIFXIUJZUKXJXKGWNXBWNHZXJWQXAXMXJWQJZXBCEIZW
        NHZBXOEIZCLZXAXMWQXPXJWQXMXPWMWPXMXPOZWOWMWPXSWMXMWPXPWMXMWPXPXBDCEFULU
        PUMUNQRQXNXCXHLZXRXMWQXTXJWQXMXTWMWOXMXTOWPWMWOXMXTBXBDEFUOUQURRQXNXQXC
        CEIZLZXICEIZCLZXTXROZXMWQYBXJWQXMYBWMWOWPXMYBOWMWOXMWPYBWMWOXMWPYBWMWOX
        MWPJNYAXQBXBCDEFUSVGUTVAVBRQWQXMYDXJWMWPYDWOCDEXIXLFVCQVDXJXMYBYDYEOOWQ
        XTXJYBYDXRXTXJYBYDXROZOZXTXJNXCXILYAYCLZYGXCXHXISXCXICEVEYBYHYFYBYHNXQY
        CLZYFXQYAYCSYIYDXRXQYCCSTVFVHVITVJVKVLVMWTXRAXOWNWRXOLWSXQCWRXOBEVNVOVP
        VQUPVRVIWHWQXGAGWNWNXEWQWRWNHZXMNZXFXDWTWQYKXFOOZWTYLOCXCWTCXCLZYLWTYMN
        WSXCLZYLWSCXCSWQYKYNXFWQYKYNXFOZWQYKNWMWOYJXMYOWMWOWPYKVSWMWOWPYKVTWQYJ
        XMWAWQYJXMWBWMWOYJXMJNYNXFBWRXBDEFWCWDWITWEVFVHWFRWGWJWTXDAGWNXFWSXCCWR
        XBBEVNVOWKWL $.
        $( [15-Sep-2013] $)
    $}

    ${
      $d x y +w $.  $d x y A $.  $d x y B $.  $d x y N $.
      $( Existential uniqueness of vector negatives.  (Contributed by FL,
         29-May-2014.) $)
      negveudr $p |- ( ( N e. NN /\ A e. ( RR ^m ( 1 ... N ) )
      /\ B e. ( RR ^m ( 1 ... N ) ) )
          -> E! x e. ( RR ^m ( 1 ... N ) ) ( A +w x ) = B ) $=
        ( vy wcel cr co w3a wceq wa wi 3adant2 impcom cc eqtr wf elmap cfz cmap
        cn c1 cv wrex weq wral wreu c0cv cfv eqid rnegvex2b claddrvr 3exp com23
        3simpa imp id wss ax-resscn cnex ovex mapss ax-mp addcomv syl3an 3expia
        sseli 3adant3 addassv eqcomd expcom com12 3expd addidv2 sylan2 3ad2ant3
        com34 3imp oveq1 syl 3syl com4l 3ad2ant2 mp2d mpd oveq2 rcla4ev syl2anc
        ex eqeq1d rexlimiv pm2.43i simpl1 adantr reex 3imtr4i ad2antrl ad2antll
        fss mpan2 addcanrg biimpd syl13anc com3r eqcoms ralrimivv reu4 sylanbrc
        com3l ) DUCHZBIUDDUAJZUBJZHZCXNHZKZBAUEZEJZCLZAXNUFZXTBGUEZEJZCLZMZAGUG
        ZNZGXNUHAXNUHXTAXNUIXQYAXQXLXOMYBBEJZDUJUKZLZGXNUFXQYANZXLXOXPUQGBDEYIF
        YIULZUMYJYKGXNYBXNHZYJXQYAYMYJXQKZYBCEJZXNHZBYOEJZCLZYAYMXQYPYJXQYMYPXL
        XPYMYPNZXOXLXPYSXLYMXPYPXLYMXPYPYBDCEFUNUOUPUROPOYNYCYHLZYRYMXQYTYJXQYM
        YTXLXOYMYTNXPXLXOYMYTXLXLXOBQXMUBJZHZYMYBUUAHZYTXLUSXNUUABIQUTZXNUUAUTV
        AIQXMVBUDDUAVCZVDVEZVIZXNUUAYBUUFVIZBYBDEFVFVGVHVJPOYNYQYCCEJZLZYICEJZC
        LZYTYRNZYMXQUUJYJXQYMUUJXLXOXPYMUUJNXLXOYMXPUUJXLXOYMXPUUJXOYMXPKXLUUJX
        OUUBYMUUCXPCUUAHZXLUUJNUUGUUHXNUUACUUFVIZXLUUBUUCUUNKZUUJXLUUPMUUIYQBYB
        CDEFVKVLVMVGVNVOVSVTPOXQYMUULYJXLXPUULXOXPXLUUNUULUUOCDEYIYLFVPVQOVRYJY
        MUUJUULUUMNNXQYTYJUUJUULYRYTYJUUJUULYRNZNZYTYJMYCYILUUIUUKLZUURYCYHYIRY
        CYICEWAUUJUUSUUQUUJUUSMYQUUKLZUUQYQUUIUUKRUUTUULYRYQUUKCRWKWBVMWCWKWDWE
        WFWGXTYRAYOXNXRYOLXSYQCXRYOBEWHWLWIWJUOWMWCWNXQYGAGXNXNYEXQXRXNHZYMMZYF
        YDXTXQUVBYFNNZXTUVCNCYCXTCYCLZUVCXTUVDMXSYCLZUVCXSCYCRXQUVBUVEYFXQUVBUV
        EYFNZXQUVBMXLUUBXRUUAHZUUCUVFXLXOXPUVBWOXQUUBUVBXOXLUUBXPUUGWEWPUVAUVGX
        QYMXMIXRSZXMQXRSZUVAUVGUVHUUDUVIVAXMIQXRXAXBIXMXRWQUUETQXMXRVBUUETWRWSY
        MUUCXQUVAXMIYBSZXMQYBSZYMUUCUVJUUDUVKVAXMIQYBXAXBIXMYBWQUUETQXMYBVBUUET
        WRWTXLUUBUVGUUCKMUVEYFBXRYBDEFXCXDXEWKXFWBVMXGPXKXHXTYDAGXNYFXSYCCXRYBB
        EWHWLXIXJ $.
        $( [29-May-2014] $)
    $}

  $}

  ${
    $d w v u U $.  $d w v u n N $.  $d w u v V $.  $d w +w $.
    issubcv.1 $e |- +w = ( +cv ` N ) $.
    issubcv.2 $e |- -w = ( -cv ` N ) $.
    $( Substraction of complex vectors in a space of dimension ` n ` .
       (Contributed by FL, 15-Sep-2013.) $)
    issubcv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
                       V e. ( CC ^m ( 1 ... N ) ) )
     -> ( U -w V ) =
       ( iota_ w e. ( CC ^m ( 1 ... N ) ) ( V +w w ) = U ) ) $=
      ( vu vv vn wcel cc c1 co cmap cv wceq crio eqeq1d cn cfz cmcv cplcv cmpt2
      w3a cfv wi oveq2 oveq2d fveq2 oveqd riotaeqbidv mpt2eq123dv df-subcv ovex
      mpt2ex fvmpt cvv riotaex eqeq2 riotabidv oveq1 eqid 3exp mpii oveq imbi2d
      ovmpt2g syl5ibr syl 3imp wb adantr riotabidva eqeq2d ax-mp syl6bb sylibr
      wa ) CUALZBMNCUBOZPOZLZDWCLZUFBDCUCUGZOZDAQZCUDUGZOZBRZAWCSZRZBDFOZDWHEOZ
      BRZAWCSZRZWAWDWEWMWAWFIJWCWCJQZWHWIOZIQZRZAWCSZUEZRZWDWEWMUHZUHKCIJMNKQZU
      BOZPOZXIWSWHXGUDUGZOZXARZAXISZUEXDUAUCXGCRZIJXIXIXMWCWCXCXNXHWBMPXGCNUBUI
      UJZXOXNXLXBAXIWCXOXNXKWTXAXNXJWIWSWHXGCUDUKULTUMUNAJIKUOIJWCWCXCMWBPUPZXP
      UQURWDXFXEWEBDXDOZWLRZUHWDWEWLUSLZXRWKAWCUTWDWEXSXRIJBDWCWCXCWLXDWTBRZAWC
      SUSXABRXBXTAWCXABWTVAVBWSDRZXTWKAWCYAWTWJBWSDWHWIVCTVBXDVDVIVEVFXEWMXRWEX
      EWGXQWLBDWFXDVGTVHVJVKVLEWIRZWRWMVMGYBWRWNWLRZWMYBWQWLWNYBWPWKAWCYBWHWCLZ
      VTWOWJBYBWOWJRYDDWHEWIVGVNTVOVPFWFRZYCWMVMHYEWNWGWLBDFWFVGTVQVRVQVS $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y +w $.  $d x y A $.  $d x y B $.  $d x y C $.  $d x y N $.
    subaddv.1 $e |- +w = ( +cv ` N ) $.
    subaddv.2 $e |- -w = ( -cv ` N ) $.
    $( Relationship between subtraction and addition.  (Contributed by FL,
       30-May-2014.) $)
    subaddv $p |- ( ( N e. NN /\ ( A e. ( CC ^m ( 1 ... N ) ) /\
       B e. ( CC ^m ( 1 ... N ) ) /\ C e. ( CC ^m ( 1 ... N ) ) ) ) ->
                  ( ( A -w B ) = C <-> ( B +w C ) = A ) ) $=
      ( vx vy wcel co w3a wa wceq cv wb eqeq1d wi imp cn cc c1 cfz cmap issubcv
      crio 3adant3r3 wreu simpll negveud 3exp com3l adantl a17d riota2f syl2anc
      ax-17 oveq2 ex 3impb 3com13 impcom bitr4d ) DUAKZAUBUCDUDLUELZKZBVFKZCVFK
      ZMZNABFLZCOZBIPZELZAOZIVFUGZCOZBCELZAOZVEVGVHVLVQQVIVEVGVHMVKVPCIADBEFGHU
      FRUHVJVEVSVQQZVIVHVGVEVTSZVIVHVGWAVIVHVGNZNZVEVTWCVENVIVOIVFUIZVTVIWBVEUJ
      WCVEWDWBVEWDSZVIVHVGWEVEVHVGWDVEVHVGWDIBADEGUKULUMTUNTVOVSIJVFCJPCKIURVIV
      SIUOVMCOVNVRAVMCBEUSRUPUQUTVAVBVCVD $.
      $( [30-May-2014] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y N $.
    issubrv.2 $e |- -w = ( -cv ` N ) $.
    $( Addition of complex vectors.  (Contributed by FL, 29-May-2014.) $)
    issubrv $p |- ( ( N e. NN /\ A e. ( CC ^m ( 1 ... N ) ) /\
       B e. ( CC ^m ( 1 ... N ) ) ) ->
            ( A -w B ) = ( x e. ( 1 ... N ) |-> ( ( A ` x ) - ( B ` x ) ) ) )
        $=
      ( vy wcel cc co cfv cmin cmpt wceq cvv wi cnex ffvelrn ex sylbi cn c1 cfz
      cmap w3a cv cplcv caddc wa simpr ovex fveq2 oveq12d fvmptg sylancl oveq2d
      wf elmap 3ad2ant3 imp 3ad2ant2 pncan3 syl2anc eqtrd mpteq2dva simp1 simp3
      eqid wral jcad subcl syl6 ralrimiv fmpt sylib wb pm3.2i a1i elmapg mpbird
      syl isaddrv syl3anc simp2 feqmptd 3eqtr4d sylibr subaddv syl13anc ) DUAHZ
      BIUBDUCJZUDJZHZCWLHZUEZBCEJAWKAUFZBKZWPCKZLJZMZNZCWTDUGKZJZBNZWOGWKGUFZCK
      ZXEWTKZUHJZMZGWKXEBKZMXCBWOGWKXHXJWOXEWKHZUIZXHXFXJXFLJZUHJZXJXLXGXMXFUHX
      LXKXMOHXGXMNWOXKUJXJXFLUKAXEWSXMWKOWTWPXENWQXJWRXFLWPXEBULWPXECULUMWTVHZU
      NUOUPXLXFIHZXJIHZXNXJNWOXKXPWNWJXKXPPZWMWNWKICUQZXRIWKCQUBDUCUKZURZXSXKXP
      WKIXECRSTUSUTWOXKXQWMWJXKXQPZWNWMWKIBUQZYBIWKBQXTURZYCXKXQWKIXEBRSTVAUTXF
      XJVBVCVDVEWOWJWNWTWLHZXCXINWJWMWNVFZWJWMWNVGZWOYEWKIWTUQZWOWSIHZAWKVIYHWO
      YIAWKWOWPWKHZWQIHZWRIHZUIYIWOYJYKYLWMWJYJYKPZWNWMYCYMYDYCYJYKWKIWPBRSTVAW
      NWJYJYLPZWMWNXSYNYAXSYJYLWKIWPCRSTUSVJWQWRVKVLVMAWKIWSWTXOVNVOZWOIOHZWKOH
      ZUIZYEYHVPYRWOYPYQQXTVQVRIWKWTOOVSWAVTGCDWTXBXBVHZWBWCWOGWKIBWOWMYCWJWMWN
      WDZYDVOWEWFWOWJWMWNYEXAXDVPYFYTYGWOYHYEYOIWKWTQXTURWGBCWTDXBEYSFWHWIVT $.
      $( [29-May-2014] $)
  $}

  ${
    $d x U $.  $d x N $.  $d x V $.
    subclcvd.1 $e |- -w = ( -cv ` N ) $.
    $( Closure law for vector substraction.  (Contributed by FL,
       15-Sep-2013.) $)
    subclcvd $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
       V e. ( CC ^m ( 1 ... N ) ) )
       -> ( U -w V ) e. ( CC ^m ( 1 ... N ) ) ) $=
      ( vx cn wcel cc c1 cfz co cmap w3a cv cplcv cfv wceq crio eqid wreu sylib
      issubcv negveud 3com23 ovex riotaclb eqeltrd ) BGHZAIJBKLZMLZHZCUKHZNZACD
      LCFOBPQZLARZFUKSZUKFABCUODUOTZEUCUNUPFUKUAZUQUKHUIUMULUSFCABUOURUDUEUPFUK
      IUJMUFUGUBUH $.
      $( [15-Sep-2013] $)

    $( Closure law for vector substraction.  (Contributed by FL,
       29-May-2014.) $)
    subclrvd $p |- ( ( N e. NN /\ U e. ( RR ^m ( 1 ... N ) ) /\
       V e. ( RR ^m ( 1 ... N ) ) )
       -> ( U -w V ) e. ( RR ^m ( 1 ... N ) ) ) $=
      ( vx wcel cr c1 cfz co cmap w3a cfv cc wss wf wi reex cvv cn cv cmin cmpt
      wceq id ax-resscn cnex ovex mapss ax-mp sseli issubrv syl3an wral ffvelrn
      elmap 3adant1 3adant2 resubcl syl2anc 3exp sylbi com12 ralrimiv eqid fmpt
      wa imp sylib wb pm3.2i a1i elmapg syl mpbird eqeltrd ) BUAGZAHIBJKZLKZGZC
      VTGZMZACDKZFVSFUBZANZWECNZUCKZUDZVTVRVRWAAOVSLKZGWBCWJGWDWIUEVRUFVTWJAHOP
      VTWJPUGHOVSUHIBJUIZUJUKZULVTWJCWLULFACBDEUMUNWCWIVTGZVSHWIQZWCWHHGZFVSUOZ
      WNWAWBWPVRWAWBVHWOFVSWAWBWEVSGZWORZWAVSHAQZWBWRRHVSASWKUQWBWSWRWBVSHCQZWS
      WRRHVSCSWKUQWTWSWQWOWTWSWQMWFHGZWGHGZWOWSWQXAWTVSHWEAUPURWTWQXBWSVSHWECUP
      USWFWGUTVAVBVCVDVCVIVEURFVSHWHWIWIVFVGVJWCHTGZVSTGZVHZWMWNVKXEWCXCXDSWKVL
      VMHVSWITTVNVOVPVQ $.
      $( [29-May-2014] $)
  $}

  $c -ucv $. $( Negative of a complex vector. $)

  $( Extends class notation with the negative of a complex vector. $)
  cnegcv $a class -ucv $.

  ${
    $d n u $.
    $( Negative of a complex vector.  Experimental. $)
    df-ucv $a |- -ucv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) )
      |-> ( ( 0cv ` n ) ( -cv ` n ) u ) ) ) $.
  $}

  ${
    $d u U $.  $d u n N $.
    isucv.1 $e |- ~w = ( -ucv ` N ) $.
    isucv.2 $e |- 0w = ( 0cv ` N ) $.
    isucv.3 $e |- -w = ( -cv ` N ) $.
    $( Negative of a complex vector.  (Contributed by FL, 15-Sep-2013.) $)
    isucv $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) ) ->
       ( ~w ` U ) = ( 0w -w U ) ) $=
      ( vu vn cfv wceq wcel cc c1 cfz co cmap wi cvv cnegcv cn wa c0cv cv simpl
      cmcv cmpt mptex oveq2 oveq2d fveq2 eqidd oveq123d mpteq12dv df-ucv fvmptg
      ovex sylancl fveq1d simpr eqid eqtrd oveq eqeq2d ax-mp oveq1 fveq1 eqeq1d
      syl5ibr ) EBUAKZLZBUBMZANOBPQZRQZMZUCZAEKZDACQZLZSFVQVTVLAVKKZVSLZDBUDKZL
      ZVQWBSGVQWBWDWAWCACQZLZCBUGKZLZVQWFSHVQWFWHWAWCAWGQZLVQWAAIVOWCIUEZWGQZUH
      ZKZWIVQAVKWLVQVMWLTMVKWLLVMVPUFIVOWKNVNRURUIJBINOJUEZPQZRQZWNUDKZWJWNUGKZ
      QZUHWLUBTUAWNBLZIWPWSVOWKWTWOVNNRWNBOPUJUKWTWQWCWJWJWRWGWNBUGULWNBUDULWTW
      JUMUNUOIJUPUQUSUTVQVPWITMWMWILVMVPVAWCAWGURIAWKWIVOTWLWJAWCWGUJWLVBUQUSVC
      WHWEWIWAWCACWGVDVEVJVFWDVSWEWADWCACVGVEVJVFVLVRWAVSAEVKVHVIVJVF $.
      $( [15-Sep-2013] $)
  $}

  ${
    isucvr.1 $e |- ~w = ( -ucv ` N ) $.
    $( Negative of a complex vector.  (Contributed by FL, 29-May-2014.) $)
    isucvr $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) ) ->
       ( ~w ` U ) e. ( CC ^m ( 1 ... N ) ) ) $=
      ( cn wcel cc c1 cfz co cmap cfv c0cv cmcv eqid isucv simpl zernpl adantr
      wa simpr subclcvd syl3anc eqeltrd ) BEFZAGHBIJKJZFZTZACLBMLZABNLZJZUFABUJ
      UICDUIOZUJOZPUHUEUIUFFZUGUKUFFUEUGQUEUNUGBUIULRSUEUGUAUIBAUJUMUBUCUD $.
      $( [29-May-2014] $)
  $}

  $c .cv $. $( Multiplication of complex vectors by a scalar. $)

  $( Extends class notation with scalar multiplication of complex vectors. $)
  csmcv $a class .cv $.

  ${
    $d n s u x $.
    $( Multiplication of complex vectors by a scalar in a space of dimension
       ` n ` .  Experimental. $)
    df-mulcv $a |- .cv = ( n e. NN |->
      ( s e. CC , u e. ( CC ^m ( 1 ... n ) ) |->
        ( x e. ( 1 ... n ) |-> ( s x. ( u ` x ) ) ) ) ) $.
  $}

  ${
    ismulcv.1 $e |- .t = ( .cv ` N ) $.
    ${
      $d n s u x N $.  $d s u x S $.  $d s u x U $.
      $( Multiplication of complex vectors by a scalar in a space of dimension
         ` n ` .  (Contributed by FL, 15-Sep-2013.) $)
      ismulcv $p |- ( ( N e. NN /\ S e. CC /\ U e. ( CC ^m ( 1 ... N ) ) )
       -> ( S .t U ) = ( x e. ( 1 ... N ) |-> ( S x. ( U ` x ) ) ) ) $=
        ( vs vu vn wceq wcel cc c1 cfz co cmap cv cmul cmpt cvv csmcv cfv cn wi
        cmpt2 simp1 cnex ovex mpt2ex eqidd oveq2 mpteq12dv mpt2eq123dv df-mulcv
        oveq2d fvmptg sylancl oveqd simp2 simp3 mptex oveq1 mpteq2dv fveq1 eqid
        w3a a1i ovmpt2g syl3anc eqtrd oveq eqeq1d syl5ibr ax-mp ) EDUAUBZJZDUCK
        ZBLKZCLMDNOZPOZKZVFZBCEOZAVSBAQZCUBZROZSZJZUDFWBWHVPBCVOOZWGJWBWIBCGHLV
        TAVSGQZWDHQZUBZROZSZUEZOZWGWBVOWOBCWBVQWOTKVOWOJVQVRWAUFGHLVTWNUGLVSPUH
        UIIDGHLLMIQZNOZPOZAWRWMSZUEWOUCTUAWQDJZGHLWSWTLVTWNXALUJXAWRVSLPWQDMNUK
        ZUOXAAWRWMVSWMXBXAWMUJULUMAHIGUNUPUQURWBVRWAWGTKZWPWGJVQVRWAUSVQVRWAUTX
        CWBAVSWFMDNUHVAVGGHBCLVTWNWGWOAVSBWLROZSTWJBJAVSWMXDWJBWLRVBVCWKCJZAVSX
        DWFXEWLWEBRWDWKCVDUOVCWOVEVHVIVJVPWCWIWGBCEVOVKVLVMVN $.
        $( [23-Aug-2014] $) $( [15-Sep-2013] $)
    $}

    ${
      $d x N $.  $d x S $.  $d x U $.
      $( Closure of scalar multiplication.  (Contributed by FL,
         29-May-2014.) $)
      clsmulcv $p |- ( ( N e. NN /\ S e. CC /\ U e. ( CC ^m ( 1 ... N ) ) )
       -> ( S .t U ) e. ( CC ^m ( 1 ... N ) ) ) $=
        ( vx cn wcel cc c1 cfz co cmap w3a wf wi cnex wa syl cvv cv cfv ismulcv
        cmul cmpt wral ovex elmap ffvelrn mulcl expcom ex com23 a1d sylbi com3l
        3imp ralrimiv eqid fmpt sylib wb pm3.2i a1i elmapg mpbird eqeltrd ) CGH
        ZAIHZBIJCKLZMLZHZNZABDLFVJAFUAZBUBZUDLZUEZVKFABCDEUCVMVQVKHZVJIVQOZVMVP
        IHZFVJUFVSVMVTFVJVHVIVLVNVJHZVTPZVLVHVIWBVLVJIBOZVHVIWBPZPIVJBQJCKUGZUH
        WCWDVHWCWAVIVTWCWAVIVTPZWCWARVOIHZWFVJIVNBUIVIWGVTAVOUJUKSULUMUNUOUPUQU
        RFVJIVPVQVQUSUTVAVMITHZVJTHZRZVRVSVBWJVMWHWIQWEVCVDIVJVQTTVESVFVG $.
        $( [29-May-2014] $)
    $}

    ${
      $d x N $.  $d x S $.  $d x U $.
      $( Closure of scalar multiplication.  (Contributed by FL,
         29-May-2014.) $)
      clsmulrv $p |- ( ( N e. NN /\ S e. RR /\ U e. ( RR ^m ( 1 ... N ) ) )
       -> ( S .t U ) e. ( RR ^m ( 1 ... N ) ) ) $=
        ( vx wcel cr c1 cfz co cmap w3a cc wf reex elmap wa 3adant1 cvv cn cmul
        cv cfv cmpt wceq recn wss ax-resscn fss mpan2 ovex cnex 3imtr4i ismulcv
        id syl3an wral wi simp1 ffvelrn sylanb jca 3expia remulcl syl6 ralrimiv
        eqid fmpt sylib wb pm3.2i a1i elmapg syl mpbird eqeltrd ) CUAGZAHGZBHIC
        JKZLKZGZMZABDKZFVTAFUCZBUDZUBKZUEZWAVRVRVSANGWBBNVTLKGZWDWHUFVRUPAUGVTH
        BOZVTNBOZWBWIWJHNUHWKUIVTHNBUJUKHVTBPICJULZQZNVTBUMWLQUNFABCDEUOUQWCWHW
        AGZVTHWHOZWCWGHGZFVTURWOWCWPFVTWCWEVTGZVSWFHGZRZWPVSWBWQWSUSVRVSWBWQWSV
        SWBWQMVSWRVSWBWQUTWBWQWRVSWBWJWQWRWMVTHWEBVAVBSVCVDSAWFVEVFVGFVTHWGWHWH
        VHVIVJWCHTGZVTTGZRZWNWOVKXBWCWTXAPWLVLVMHVTWHTTVNVOVPVQ $.
        $( [29-May-2014] $)
    $}

    ${
      $d n s u x N $.
      $( Functionality of scalar multiplication.  (Contributed by FL,
         29-May-2014.) $)
      fnmulcv $p |- ( N e. NN -> .t : ( CC X. ( CC ^m ( 1 ... N ) ) )
          --> ( CC ^m ( 1 ... N ) ) ) $=
        ( vs vu vx vn wcel cc c1 cfz co cmap wf cv wral wa syl cvv wceq cxp cfv
        cn cmul cmpt cmpt2 cnex ovex elmap ffvelrn mulcl expcom ralrimdva sylbi
        wi a1dd impcom eqid fmpt sylib wb pm3.2i elmapg mpbird ralrimivva fmpt2
        a1i csmcv mpt2exga eqidd oveq2 oveq2d mpteq1 mpt2eq123dv df-mulcv mpdan
        fvmptg syl5eq feq1d ) AUCHZIIJAKLZMLZUAZWBBNWCWBDEIWBFWADOZFOZEOZUBZUDL
        ZUEZUFZNZVTWIWBHZEWBPDIPWKVTWLDEIWBVTWDIHZWFWBHZQZQZWLWAIWINZWPWHIHZFWA
        PZWQWOVTWSWNWMVTWSUOZWNWAIWFNZWMWTUOIWAWFUGJAKUHZUIXAWMWSVTXAWMWRFWAXAW
        EWAHQWGIHZWMWRUOWAIWEWFUJWMXCWRWDWGUKULRUMUPUNUQUQFWAIWHWIWIURUSUTWPISH
        ZWASHZQZWLWQVAXFWPXDXEUGXBVBVGIWAWISSVCRVDVEDEIWBWIWBWJWJURVFUTVTWCWBBW
        JVTBAVHUBZWJCVTWJSHZXGWJTVTXDWBSHZQZXHXJVTXDXIUGIWAMUHVBVGDEIWBWISSVIRG
        ADEIIJGOZKLZMLZFXLWHUEZUFWJUCSVHXKATZDEIXMXNIWBWIXOIVJXOXLWAIMXKAJKVKZV
        LXOXLWATXNWITXPFXLWAWHVMRVNFEGDVOVQVPVRVSVD $.
        $( [29-May-2014] $)
    $}

  $}

  ${
    mulone.1 $e |- .t = ( .cv ` N ) $.
    ${
      $d x N $.  $d x U $.
      $( Multiplication of a vector by 1.  (Contributed by FL, 29-May-2014.) $)
      mulone $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) )
       -> ( 1 .t U ) = U ) $=
        ( vx cn wcel cc c1 cfz co cmap wa cv cfv cmul cmpt wceq wf simpr mulid2
        cnex ovex elmap sylib ffvelrn sylan syl mpteq2dva ax-1cn ismulcv mp3an2
        feqmptd 3eqtr4d ) BFGZAHIBJKZLKGZMZEUPIENZAOZPKZQZEUPUTQIACKZAUREUPVAUT
        URUSUPGZMUTHGZVAUTRURUPHASZVDVEURUQVFUOUQTHUPAUBIBJUCUDUEZUPHUSAUFUGUTU
        AUHUIUOIHGUQVCVBRUJEIABCDUKULUREUPHAVGUMUN $.
        $( [9-Jan-2015] $) $( [29-May-2014] $)
    $}

    ${
      $d u x .t $.  $d u x N $.
      $( Vector addition is onto.  (Contributed by FL, 29-May-2014.) $)
      vecscmonto $p |- ( N e. NN -> .t : ( CC X. ( CC ^m ( 1 ... N ) ) )
       -onto-> ( CC ^m ( 1 ... N ) ) ) $=
        ( vu vx cn wcel cc c1 cfz co cmap cxp wf cv cfv wceq wrex wral sylanbrc
        wfo fnmulcv cop ax-1cn a1i simpr vex opelxp df-ov mulone syl5reqr fveq2
        wa eqeq2d rcla4ev syl2anc ralrimiva dffo3 ) AFGZHHIAJKLKZMZUTBNDOZEOZBP
        ZQZEVARZDUTSVAUTBUAABCUBUSVFDUTUSVBUTGZUMZIVBUCZVAGZVBVIBPZQZVFVHIHGZVG
        VJVMVHUDUEUSVGUFIVBHUTDUGUHTVHVKIVBBKVBIVBBUIVBABCUJUKVEVLEVIVAVCVIQVDV
        KVBVCVIBULUNUOUPUQEDVAUTBURT $.
        $( [29-May-2014] $)
    $}

    ${
      $d x .t $.  $d x N $.  $d x S $.  $d x T $.  $d x U $.
      $( Connection between multiplication of complex numbers and scalar
         multiplication.  (Contributed by FL, 29-May-2014.) $)
      mulmulvec $p |- ( ( N e. NN /\ S e. CC /\
      ( T e. CC /\ U e. ( CC ^m ( 1 ... N ) ) ) )
      -> ( ( S x. T ) .t U ) = ( S .t ( T .t U ) ) ) $=
        ( vx wcel cc c1 cfz co wa cmul cfv cmpt cvv wceq syl3anc ismulcv cn w3a
        cmap cv simpr ovex fvmpt2 sylancl eqcomd oveq2d simpl2 simpl3l wf elmap
        eqid biimpi adantl 3ad2ant3 ffvelrn sylan mulass simpl1 simpl3r 3eqtr4d
        cnex fveq1d simp1 mulcl adantrr 3adant1 simp3r simp3l clsmulcv syld3an3
        mpteq2dva ) DUAHZAIHZBIHZCIJDKLZUCLZHZMZUBZGVSABNLZGUDZCOZNLZPZGVSAWEBC
        ELZOZNLZPZWDCELZAWIELZWCGVSWGWKWCWEVSHZMZABWFNLZNLZAWEGVSWQPZOZNLWGWKWP
        WQWTANWPWTWQWPWOWQQHWTWQRWCWOUEBWFNUFGVSWQQWSWSUOUGUHUIUJWPVQVRWFIHZWGW
        RRVPVQWBWOUKVRWAVPVQWOULZWCVSICUMZWOXAWBVPXCVQWAXCVRWAXCIVSCVEJDKUFUNUP
        UQURVSIWECUSUTABWFVASWPWJWTANWPWEWIWSWPVPVRWAWIWSRVPVQWBWOVBXBVRWAVPVQW
        OVCGBCDEFTSVFUJVDVOWCVPWDIHZWAWMWHRVPVQWBVGZVQWBXDVPVQVRXDWAABVHVIVJVPV
        QVRWAVKZGWDCDEFTSVPVQWBWIVTHZWNWLRWCVPVRWAXGXEVPVQVRWAVLXFBCDEFVMSGAWID
        EFTVNVD $.
        $( [29-May-2014] $)
    $}

    ${
      distmlva.1 $e |- +w = ( +cv ` N ) $.
      ${
        $d x +w $.  $d x .t $.  $d x N $.  $d x S $.  $d x U $.  $d x V $.
        $( Distribution of scalar multiplication over vector addition.
           (Contributed by FL, 29-May-2014.) $)
        distmlva $p |- ( ( N e. NN /\ S e. CC /\
      ( U e. ( CC ^m ( 1 ... N ) ) /\ V e. ( CC ^m ( 1 ... N ) ) ) )
      -> ( S .t ( U +w V ) ) = ( ( S .t U ) +w ( S .t V ) ) ) $=
          ( vx wcel cc co wa cfv cmul cmpt caddc wceq wi cvv cn c1 cfz cmap w3a
          cv wf wb cnex ovex elmapg anbi12d mp2an ffvelrn expcom im2anan9 simpr
          simpll simplr 3jca a1d syl6 anidms com4l sylbi com3l 3imp1 adddi eqid
          syl fvmpt2 sylancl oveq2d oveq12d 3eqtr4d simpl simprl simprr 3adant2
          adantr isaddrv fveq1d ismulcv simpl1 simpl2 simpl3r syl3anc mpteq2dva
          ex 3adant3r claddrv 3expb syld3an3 simp1 clsmulcv 3adant3l ) CUAJZAKJ
          ZBKUBCUCLZUDLZJZDWTJZMZUEZIWSAIUFZBDFLZNZOLZPZIWSXEABELZNZXEADELZNZQL
          ZPZAXFELZXJXLFLZXDIWSXHXNXDXEWSJZMZAXEIWSXEBNZXEDNZQLZPZNZOLZXEIWSAXT
          OLZPZNZXEIWSAYAOLZPZNZQLZXHXNXSAYBOLZYFYIQLZYEYLXSWRXTKJZYAKJZUEZYMYN
          RWQWRXCXRYQXCWQWRXRYQSZXCWSKBUGZWSKDUGZMZWQWRYRSSKTJZWSTJZXCUUAUHUIUB
          CUCUJUUBUUCMXAYSXBYTKWSBTTUKKWSDTTUKULUMXRUUAWQWRYQXRUUAWQWRYQSZSZSXR
          XRMUUAYOYPMZUUEXRYSYOXRYTYPYSXRYOWSKXEBUNUOYTXRYPWSKXEDUNUOUPUUFUUDWQ
          UUFWRYQUUFWRMWRYOYPUUFWRUQYOYPWRURYOYPWRUSUTWIVAVBVCVDVEVFVGAXTYAVHVJ
          XSYDYBAOXSXRYBTJYDYBRXDXRUQZXTYAQUJIWSYBTYCYCVIVKVLVMXSYHYFYKYIQXSXRY
          FTJYHYFRUUGAXTOUJIWSYFTYGYGVIVKVLXSXRYITJYKYIRUUGAYAOUJIWSYITYJYJVIVK
          VLVNVOXSXGYDAOXSXEXFYCXSWQXAXBUEZXFYCRXDUUHXRWQXCUUHWRWQXCMWQXAXBWQXC
          VPWQXAXBVQWQXAXBVRUTVSVTIBCDFHWAVJWBVMXSXKYHXMYKQXSXEXJYGXDXJYGRZXRWQ
          WRXAUUIXBIABCEGWCWJVTWBXSXEXLYJXSWQWRXBXLYJRWQWRXCXRWDWQWRXCXRWEXAXBW
          QWRXRWFIADCEGWCWGWBVNVOWHWQWRXCXFWTJZXPXIRWQXCUUJWRWQXAXBUUJBCDFHWKWL
          VSIAXFCEGWCWMXDWQXJWTJZXLWTJZXQXORWQWRXCWNWQWRXAUUKXBABCEGWOWJWQWRXBU
          ULXAADCEGWOWPIXJCXLFHWAWGVO $.
          $( [29-May-2014] $)
      $}

      ${
        $d x .t $.  $d x N $.  $d x S $.  $d x T $.  $d x U $.
        $( "Distribution" of scalar addition.  (Contributed by FL,
           29-May-2014.) $)
        distsava $p |- ( ( N e. NN /\ S e. CC /\
      ( T e. CC /\ U e. ( CC ^m ( 1 ... N ) ) ) )
      -> ( ( S + T ) .t U ) = ( ( S .t U ) +w ( T .t U ) ) ) $=
          ( vx wcel cc co w3a caddc cfv cmul cmpt cvv wceq wi cn c1 cfz cmap wa
          cv simpr ovex eqid fvmpt2 sylancl eqcomd cnex elmap ffvelrn 3exp a1dd
          wf simp2 syl com12 anabsi7 ex sylbi com25 impcom com3l oveq12d simpl2
          3imp1 simpl3l adantl 3ad2ant3 adddir syl3anc simp1 simp3r 3jca adantr
          imp ismulcv fveq1d simp3l 3eqtr4d mpteq2dva adantrd a1i 3imp clsmulcv
          addcl 3adant3l 3expb 3adant2 isaddrv ) DUAJZAKJZBKJZCKUBDUCLZUDLZJZUE
          ZMZIWRABNLZIUFZCOZPLZQZIWRXDACELZOZXDBCELZOZNLZQZXCCELZXHXJFLZXBIWRXF
          XLXBXDWRJZUEZAXEPLZBXEPLZNLZXDIWRXRQZOZXDIWRXSQZOZNLXFXLXQXRYBXSYDNXQ
          YBXRXQXPXRRJYBXRSXBXPUGAXEPUHIWRXRRYAYAUIUJUKULWOWPXAXPXSYDSZXAWOWPXP
          YETZWTWQWOWPYFTTWTXPWOWPWQYEWTWRKCURZXPWOWPWQYETZTZTZTZKWRCUMUBDUCUHU
          NZYGXPYJYGXPYJYGXPUEZXPYJYMXEKJZYKWRKXDCUOZYNXPYIWOYNXPYHWPYNXPWQYEYN
          XPWQMZYDXSYPXPXSRJYDXSSYNXPWQUSBXEPUHIWRXSRYCYCUIUJUKULUPUQUQUTVAVBVC
          VDVEVFVGVJVHXQWPWQYNXFXTSWOWPXAXPVIWQWTWOWPXPVKXBXPYNXAWOXPYNTZWPWTYQ
          WQWTYGYQYLYGXPYNYOVCVDVLVMVTABXEVNVOXQXIYBXKYDNXQXDXHYAXQWOWPWTMZXHYA
          SXBYRXPXBWOWPWTWOWPXAVPZWOWPXAUSWOWPWQWTVQZVRVSIACDEGWAUTWBXQXDXJYCXQ
          WOWQWTMZXJYCSXBUUAXPXBWOWQWTYSWOWPWQWTWCYTVRVSIBCDEGWAUTWBVHWDWEXBWOX
          CKJZWTXNXGSYSWOWPXAUUBWPXAUUBTTWOWPWQUUBWTWPWQUUBABWJVCWFWGWHYTIXCCDE
          GWAVOXBWOXHWSJZXJWSJZXOXMSYSWOWPWTUUCWQACDEGWIWKWOXAUUDWPWOWQWTUUDBCD
          EGWIWLWMIXHDXJFHWNVOWD $.
          $( [29-May-2014] $)
      $}
    $}

  $}

  ${
    $d s u v +w $.  $d s u v .t $.  $d s u v N $.  $d u v w x y +w $.
    $d w x y N $.
    tcnvec.1 $e |- +w = ( +cv ` N ) $.
    tcnvec.2 $e |- .t = ( .cv ` N ) $.
    $( Nuples of complex numbers has a structure of vector space.  (Contributed
       by FL, 29-May-2014.) $)
    tcnvec $p |- ( N e. NN -> <. +w , .t >. e. CVec ) $=
      ( vu vs vv vw vx vy wcel cc co cv wceq wral wa wrex raleqbi1dv cn cop cvc
      cablo c1 cfz cmap cxp caddc cmul cgr sigadd w3a claddrv 3adant3r3 addassv
      wf jca ralrimivvva c0cv cfv eqid zernpl addidv2 cnegvex2b ralrimiva oveq1
      adantr eqeq1d simpl eqeq2d rexbidv anbi12d ralbidva rcla4ev syl2anc cplcv
      wfo crn wb vecaddonto forn cvv fvex eqeltri rneqi eqcomi isgrpo2 ax-mp id
      xpeq12d feq23d eleq2 anbi1d rexeq anbi2d rexeqbi1dv 3anbi123d syl5bb 3syl
      mpbir3and addcomv 3expib ralrimivv raleq eqcoms isablo syl6rbbr mpbir2and
      fnmulcv mulone simplll simplr simpr distmlva syl112anc distsava mulmulvec
      simpllr xpeq2 ralbidv 3anbi23d isvc ) AUALZCBUBUCLZCUDLZMMUEAUFNUGNZUHZYG
      BUQZUEFOZBNYJPZGOZYJHOZCNZBNYLYJBNZYLYMBNCNPZHYGQZYLYMUINYJBNYOYMYJBNZCNP
      ZYLYMUJNYJBNYLYRBNPZRZHMQZRZGMQZRZFYGQZYDYFCUKLZYNYMYJCNPZHYGQZFYGQZYDUUG
      YGYGUHZYGCUQZYNYGLZYNIOZCNYJYMUUNCNCNPZRZIYGQZHYGQZFYGQZJOZYJCNZYJPZKOYJC
      NZUUTPZKYGSZRZFYGQZJYGSZACDULYDUUPFHIYGYGYGYDYJYGLZYMYGLZUUNYGLZUMRUUMUUO
      YDUVIUVJUUMUVKYJAYMCDUNUOYJYMUUNACDUPURUSYDAUTVAZYGLUVLYJCNZYJPZUVCUVLPZK
      YGSZRZFYGQZUVHAUVLUVLVBZVCYDUVQFYGYDUVIRZUVNUVPYJACUVLUVSDVDKYJACUVLDUVSV
      EURVFUVGUVRJUVLYGUUTUVLPZUVFUVQFYGUWAUVIRZUVBUVNUVEUVPUWBUVAUVMYJUWAUVAUV
      MPUVIUUTUVLYJCVGVHVIUWBUVDUVOKYGUWBUUTUVLUVCUWAUVIVJVKVLVMVNVOVPYDUUKYGAV
      QVAZVRZUWCVSZYGPZUUGUULUUSUVHUMZVTAUWCUWCVBWAZUUKYGUWCWBZUUGUWEUWEUHZUWEC
      UQZYNUWELZUUORZIUWEQZHUWEQZFUWEQZUVBUVDKUWESZRZFUWEQZJUWESZUMZUWFUWGCWCLU
      UGUXAVTCUWCWCDAVQWDWEFHIJWCKCUWECVSZUWECUWCDWFWGZWHWIUWFUWKUULUWPUUSUWTUV
      HUWFUWJUWEUUKYGCUWFUWEYGUWEYGUWFWJZUXDWKUXDWLUWOUURFUWEYGUWNUUQHUWEYGUWMU
      UPIUWEYGUWFUWLUUMUUOUWEYGYNWMWNTTTUWSUVGJUWEYGUWRUVFFUWEYGUWFUWQUVEUVBUVD
      KUWEYGWOWPTWQWRWSWTXAYDUUHFHYGYGYDUVIUVJUUHYJYMACDXBXCXDYDUWDUWFYFUUGUUJR
      ZVTUWHUWIUWFUXEUUGUUHHUWEQZFUWEQZRYFUWFUUJUXGUUGUUJUXGVTYGUWEUUIUXFFYGUWE
      UUHHYGUWEXETXFWPFHCUWEUXCXGXHWTXIABEXJYDUUEFYGUVTYKUUDYJABEXKUVTUUCGMUVTY
      LMLZRZYQUUBUXIYPHYGUXIUVJRYDUXHUVIUVJYPYDUVIUXHUVJXLUVTUXHUVJXMYDUVIUXHUV
      JXSUXIUVJXNYLYJAYMBCEDXOXPVFUXIUUAHMUXIYMMLZRZYSYTUXKYDUXHUXJUVIYSYDUVIUX
      HUXJXLZUVTUXHUXJXMZUXIUXJXNZYDUVIUXHUXJXSZYLYMYJABCEDXQXPUXKYDUXHUXJUVIYT
      UXLUXMUXNUXOYLYMYJABEXRXPURVFURVFURVFYDUUKYGCVRUXBYGPYEYFYIUUFUMZVTZACDWA
      UUKYGCWBUXQYGUXBYGUXBPZUXPYFMUXBUHZUXBBUQZYKYPHUXBQZUUBRZGMQZRZFUXBQZUMYE
      UXRYIUXTUUFUYEYFUXRYHYGUXSUXBBYGUXBMXTUXRWJWLUUEUYDFYGUXBUXRUUDUYCYKUXRUU
      CUYBGMUXRYQUYAUUBYPHYGUXBXEWNYAWPTYBFGHBCUXBUXBVBYCXHXFWTXA $.
      $( [29-May-2014] $)
  $}

  $c /cv $. $( Division of complex vectors by a scalar. $)

  $( Extends class notation with scalar division of complex vectors. $)
  cdivcv $a class /cv $.

  ${
    $d n u s $.
    $( Division of a complex vector by a scalar in a space of dimension
       ` n ` .  Experimental. $)
    df-divcv $a |- /cv = ( n e. NN |-> ( u e. ( CC ^m ( 1 ... n ) ) ,
        s e. ( CC \ { 0 } ) |-> ( ( 1 / s ) ( .cv ` n ) u ) ) ) $.
  $}

  ${
    $d n s u N $.  $d s u .t $.  $d s u S $.  $d s u U $.
    isdivcv2.1 $e |- /t = ( /cv ` N ) $.
    isdivcv2.2 $e |- .t = ( .cv ` N ) $.
    $( Division of complex vectors by a scalar in a space of dimension ` N ` .
       (Contributed by FL, 29-May-2014.) $)
    isdivcv2 $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
     S e. ( CC \ { 0 } ) ) -> ( U /t S ) = ( ( 1 / S ) .t U ) ) $=
      ( vu vs vn wcel cc c1 cfz co cmap cfv cv wceq cvv cc0 csn cdif w3a cdivcv
      cn cdiv csmcv cmpt2 a1i oveqd simp1 ovex difexg ax-mp mpt2ex oveq2 oveq2d
      eqidd fveq2 mpt2eq123dv df-divcv fvmptg sylancl simp2 simp3 eqcomi adantl
      cnex wa simpl oveq123d eqid ovmpt2ga syl3anc 3eqtrd ) CUFKZBLMCNOZPOZKZAL
      UAUBZUCZKZUDZBAEOBACUEQZOBAHIVSWBMIRZUGOZHRZCUHQZOZUIZOZMAUGOZBDOZWDEWEBA
      EWESWDFUJUKWDWEWKBAWDVQWKTKWEWKSVQVTWCULHIVSWBWJLVRPUMLTKWBTKVILWATUNUOUP
      JCHILMJRZNOZPOZWBWGWHWOUHQZOZUIWKUFTUEWOCSZHIWQWBWSVSWBWJWTWPVRLPWOCMNUQU
      RWTWBUSWTWRWIWGWHWOCUHUTUKVAHJIVBVCVDUKWDVTWCWNTKZWLWNSVQVTWCVEVQVTWCVFXA
      WDWMBDUMUJHIBAVSWBWJWNWKTWHBSZWFASZVJZWGWMWHBWIDWIDSXDDWIGVGUJXCWGWMSXBWF
      AMUGUQVHXBXCVKVLWKVMVNVOVP $.
      $( [29-May-2014] $)
  $}

  ${
    divclcvd.1 $e |- /t = ( /cv ` N ) $.
    $( Closure law for vector division by a scalar.  (Contributed by FL,
       29-May-2014.) $)
    divclcvd $p |- ( ( N e. NN /\ U e. ( CC ^m ( 1 ... N ) ) /\
       S e. ( CC \ { 0 } ) )
       -> ( U /t S ) e. ( CC ^m ( 1 ... N ) ) ) $=
      ( cn wcel cc c1 cfz co cmap cc0 csn cdif w3a cdiv csmcv cfv eqid isdivcv2
      simp1 wne wa eldifsn reccl sylbi 3ad2ant3 simp2 clsmulcv syl3anc eqeltrd
      ) CFGZBHICJKLKZGZAHMNOGZPZBADKIAQKZBCRSZKZUNABCUSDEUSTZUAUQUMURHGZUOUTUNG
      UMUOUPUBUPUMVBUOUPAHGAMUCUDVBAHMUEAUFUGUHUMUOUPUIURBCUSVAUJUKUL $.
      $( [29-May-2014] $)

    $( Closure law for vector division by a scalar.  (Contributed by FL,
       29-May-2014.) $)
    divclrvd $p |- ( ( N e. NN /\ U e. ( RR ^m ( 1 ... N ) ) /\
       S e. ( RR \ { 0 } ) )
       -> ( U /t S ) e. ( RR ^m ( 1 ... N ) ) ) $=
      ( cn wcel cr c1 cfz co cmap cc0 cdif cc wf elmap 3imtr4i wa eldifsn csmcv
      csn w3a cdiv cfv wceq id wss ax-resscn fss reex ovex cnex wne recn anim1i
      mpan2 isdivcv2 syl3an simp1 rereccl sylbi 3ad2ant3 simp2 clsmulrv syl3anc
      eqid eqeltrd ) CFGZBHICJKZLKZGZAHMUBZNGZUCZBADKZIAUDKZBCUAUEZKZVKVIVIVLBO
      VJLKGZVNAOVMNGZVPVSUFVIUGVJHBPZVJOBPZVLVTWBHOUHWCUIVJHOBUJUQHVJBUKICJULZQ
      OVJBUMWDQRAHGZAMUNZSZAOGZWFSVNWAWEWHWFAUOUPAHMTZAOMTRABCVRDEVRVGZURUSVOVI
      VQHGZVLVSVKGVIVLVNUTVNVIWKVLVNWGWKWIAVAVBVCVIVLVNVDVQBCVRWJVEVFVH $.
      $( [29-May-2014] $)
  $}

