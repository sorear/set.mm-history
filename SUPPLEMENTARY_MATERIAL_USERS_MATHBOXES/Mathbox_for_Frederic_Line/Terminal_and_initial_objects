$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Terminal and initial objects
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c InitObj $.

  $( Extend class notation with the class of all initial objects. $)
  ciobj $a class InitObj $.

  ${
    $d x z o m $.
    $( Definition of the initial objects of a category.  Experimental. $)
    df-inob $a |- InitObj = ( x e. Cat |->
      { z e. dom ( id_ ` x ) |
        A. o e. dom ( id_ ` x ) E! m e. dom ( dom_ ` x )
        ( ( ( dom_ ` x ) ` m ) = z /\ ( ( cod_ ` x ) ` m ) = o ) } ) $.
  $}

  ${
    $d m o x z T $.  $d m M $.  $d z O $.
    isinob.1 $e |- O = dom ( id_ ` T ) $.
    isinob.2 $e |- M = dom ( dom_ ` T ) $.
    isinob.3 $e |- D = ( dom_ ` T ) $.
    isinob.4 $e |- C = ( cod_ ` T ) $.
    $( The predicate "are the initial objects of a category". $)
    isinob $p |- ( T e. Cat -> ( InitObj ` T ) = { z e. O |
        A. o e. O E! m e. M ( ( D ` m ) = z /\ ( C ` m ) = o ) } ) $=
      ( wcel cfv cv wceq cdm cid_ cvv a1i ccat ciobj cdom_ ccod_ wreu wral crab
      vx wa fvex dmex rabexg fveq2 dmeqd eleq2d fveq1d eqeq1d anbi12d reubidvag
      syl imbi12d ralbidv2 rabeqbidv df-inob fvmptg mpdan eqcomi eqtrd ) DUAMZD
      UBNZEOZDUCNZNZAOZPZVKDUDNZNZFOZPZUIZEVLQZUEZFDRNZQZUFZAWDUGZVKCNZVNPZVKBN
      ZVRPZUIZEGUEZFHUFZAHUGVIWFSMZVJWFPVIWDSMZWNWOVIWCDRUJUKTWEAWDSULUTUHDVKUH
      OZUCNZNZVNPZVKWPUDNZNZVRPZUIZEWQQZUEZFWPRNZQZUFZAXGUGWFUASUBWPDPZXHWEAXGW
      DXIXFWCWPDRUMUNZXIXEWBFXGWDXIVRXGMVRWDMZXEWBXIXGWDVRXJUOXIXCVTEXDWAXIWSVO
      XBVSXIWRVMVNXIVKWQVLWPDUCUMZUPUQXIXAVQVRXIVKWTVPWPDUDUMUPUQURXIWQVLXLUNUS
      VAVBVCUHAEFVDVEVFVIWEWMAWDHWDHPVIHWDIVGTZVIWBWLFWDHVIXKVRHMWBWLVIWDHVRXMU
      OVIVTWKEWAGVIVOWHVSWJVIVMWGVNVIVKVLCVLCPVICVLKVGTUPUQVIVQWIVRVIVKVPBVPBPV
      IBVPLVGTUPUQURWAGPVIGWAJVGTUSVAVBVCVH $.
      $( [27-Jun-2014] $)
  $}

  $c TermObj $.

  $( Extend class notation with the class of all terminal objects. $)
  ctobj $a class TermObj $.

  ${
    $d x y z o m $.
    $( Definition of the terminal objects of a category.  Experimental. $)
    df-termob $a |- TermObj = ( x e. Cat |->
       { z e. dom ( id_ ` x ) |
        A. o e. dom ( id_ ` x ) E! m e. dom ( dom_ ` x )
        ( ( ( dom_ ` x ) ` m ) = o /\ ( ( cod_ ` x ) ` m ) = z ) } ) $.
  $}

