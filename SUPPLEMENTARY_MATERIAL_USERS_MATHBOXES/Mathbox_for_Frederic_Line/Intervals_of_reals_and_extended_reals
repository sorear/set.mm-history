$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Intervals of reals and extended reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y z $.  $d w x y z $.
    $( Membership to the set of open intervals implied the existence of two
       bounds in the set of the extended reals.  (Revised by Mario Carneiro,
       3-Nov-2013.) $)
    bsi $p |- ( A e. ran (,) <->
      E. x e. RR* E. y e. RR* A = ( x (,) y ) ) $=
      ( cxr cxp cr cpw cioo wf wfn crn wcel cv co wceq wrex wb ioof ffn ovelrn
      mp2b ) DDEZFGZHIHUBJCHKLCAMBMHNOBDPADPQRUBUCHSABDDCHTUA $.
      $( [3-Nov-2013] $) $( [31-Oct-2007] $)
  $}

  $( If an open interval has an element, then ` A < B ` .  (Contributed by FL,
     14-Aug-2007.) $)
  elioo1t3 $p |- ( C e. ( A (,) B ) -> A < B ) $=
    ( cxr wcel wa cioo co clt wi elioo1 xrlttr exp3a 3com23 3expia 3impd sylbid
    wbr w3a c0 wn wceq ndmioo eleq2 noel pm2.21i syl6bi syl pm2.61i ) ADEZBDEZF
    ZCABGHZEZABIRZJZULUNCDEZACIRZCBIRZSUOABCKULUQURUSUOUJUKUQURUSUOJJZUJUQUKUTU
    JUQUKSURUSUOACBLMNOPQULUAUMTUBZUPABUCVAUNCTEZUOUMTCUDVBUOCUEUFUGUHUI $.
    $( [14-Sep-2007] $) $( [14-Aug-2007] $)

  $( An open interval with its upper bound equal to ` -oo ` is empty.
     (Contributed by FL, 12-Sep-2007.) $)
  oisbmi $p |- ( A (,) -oo ) = (/) $=
    ( cxr wcel cmnf wa cioo co c0 wceq cle wbr mnfle adantr ioo0 mpbird pm2.61i
    ndmioo ) ABCZDBCZEZADFGHIZTUADAJKZRUBSALMADNOADQP $.
    $( [14-Sep-2007] $) $( [12-Sep-2007] $)

  $( An open interval with its lower bound equal to ` +oo ` is empty.
     (Contributed by FL, 12-Sep-2007.) $)
  oisbmj $p |- ( +oo (,) A ) = (/) $=
    ( cpnf cxr wcel wa cioo co c0 wceq cle wbr pnfge adantl ioo0 mpbird pm2.61i
    ndmioo ) BCDZACDZEZBAFGHIZTUAABJKZSUBRALMBANOBAQP $.
    $( [14-Sep-2007] $) $( [12-Sep-2007] $)

  $( Closure of translation in a half-infinite interval.  (Contributed by FL,
     11-Sep-2007.) $)
  truni1 $p |- ( ( A e. RR* /\ D e. RR /\ 0 < D ) ->
   ( C e. ( A (,) +oo ) -> ( C + D ) e. ( A (,) +oo ) ) ) $=
    ( cxr wcel cr clt wbr w3a cpnf caddc co wa wi ex 3ad2ant1 3ad2ant2 imp rexr
    com12 cc0 cioo readdcl simpl1 adantl syl 3jca simpr2 ltaddpos2 biimpd com23
    imp31 cc wceq recn ad2antrr addcom syl2anc breqtrrd expcom 3adant1 jca sylc
    xrlttr ltpnf wb simp1 pnfxr elioo2 sylancl 3imtr4d ) ADEZCFEZUACGHZIZBFEZAB
    GHZBJGHZIZBCKLZFEZAVTGHZVTJGHZIZBAJUBLZEZVTWEEZVOVSWDVOVSMZWAWBWCVOVSWAVMVL
    VSWANVNVSVMWAVPVQVMWANVRVPVMWABCUCZOPTQRWHVLBDEZVTDEZIVQBVTGHZMWBWHVLWJWKVL
    VMVNVSUDVSWJVOVPVQWJVRBSPUEVOVSWKVMVLVSWKNVNVSVMWKVPVQVMWKNVRVPVMWKVPVMMZWA
    WKWIVTSUFOPTQRUGWHVQWLVOVPVQVRUHVOVSWLVMVNVSWLNVLVSVMVNMZWLVPVQWNWLNVRWNVPW
    LWNVPMZBCBKLZVTGVMVNVPBWPGHZVMVPVNWQVMVPVNWQNVMVPMVNWQCBUIUJOUKULWOBUMEZCUM
    EZVTWPUNVPWRWNBUOUEVMWSVNVPCUOUPBCUQURUSUTPTVARVBABVTVDVCVOVSWCVMVLVSWCNVNV
    SVMWCVPVQVMWCNVRVPVMWCWMWAWCWIVTVEUFOPTQRUGOVOVLJDEZWFVSVFVLVMVNVGZVHAJBVIV
    JVOVLWTWGWDVFXAVHAJVTVIVJVK $.
    $( [14-Sep-2007] $) $( [11-Sep-2007] $)

  $( Closure of translation in a half-infinite interval.  (Contributed by FL,
     26-Jan-2009.) $)
  truni2 $p |- ( ( D e. RR /\ 0 < D ) ->
    ( C e. ( A (,) +oo ) -> ( C + D ) e. ( A (,) +oo ) ) ) $=
    ( cpnf cioo co wcel cr cc0 clt wbr wa caddc cxr eliooxr truni1 3expib com23
    wi adantr mpcom com12 ) BADEFZGZCHGZICJKZLZBCMFUCGZANGZDNGZLUDUGUHSZBADOUIU
    DUKSUJUIUGUDUHUIUEUFUDUHSABCPQRTUAUB $.
    $( [26-Jan-2009] $)

  $( Closure of translation in a half-infinite interval.  (Contributed by FL,
     26-Jan-2009.) $)
  truni3 $p |- ( ( D e. RR /\ 0 < D ) ->
    ( C e. ( -oo (,) A ) -> ( C - D ) e. ( -oo (,) A ) ) ) $=
    ( cmnf cioo co wcel cr cc0 clt wbr wa cxr w3a elioo2 syl 3ad2ant2 rexr 3imp
    wi cmin eliooxr 3ad2ant1 elioore resubcl adantrr sylan 3adant1 mnflt simp3l
    wb syl2anc simp1r 3jca ltsubpos biimpd impancom syl5com a1i syl6bi a1dd jca
    simp3 xrlttr sylc mpbir3and 3exp mpcom com12 ) BDAEFZGZCHGZICJKZLZBCUAFZVJG
    ZDMGZAMGZLZVKVNVPTBDAUBVSVKVNVPVSVKVNNZVPVOHGZDVOJKZVOAJKZVSVKVPWAWBWCNUKVN
    DAVOOUCVKVNWAVSVKBHGZVNWABDAUDZWDVLWAVMBCUEZUFUGUHZVTWAWBWGVOUIPVTVOMGZBMGZ
    VRNVOBJKZBAJKZLWCVTWHWIVRVTWDVLWHVKVSWDVNWEQVSVKVLVMUJWDVLLWAWHWFVORPULVKVS
    WIVNVKWDWIWEBRPQVQVRVKVNUMUNVTWJWKVSVKVNWJVKVNWJTTVSVKWDVNWJWEVLWDVMWJVLWDL
    VMWJCBUOUPUQURUSSVSVKVNWKVSVKWKVNVSVKWDDBJKZWKNWKDABOWDWLWKVCUTVASVBVOBAVDV
    EVFVGVHVI $.
    $( [26-Jan-2009] $)

  $( The center belongs to a centered interval.  (Contributed by FL,
     5-Jan-2009.) $)
  cbci $p |- ( ( A e. RR /\ B e. RR+ ) ->
     A e. ( ( A - B ) (,) ( A + B ) ) ) $=
    ( cr wcel crp wa cmin co caddc cioo cxr w3a clt wbr rpre resubcl sylan2 syl
    rexr readdcl adantr 3jca ltsubrp ltaddrp jca32 wb elioo3g mpbird ) ACDZBEDZ
    FZAABGHZABIHZJHDZULKDZUMKDZAKDZLZULAMNZAUMMNZFFZUKURUSUTUKUOUPUQUKULCDZUOUJ
    UIBCDZVBBOZABPQULSRUJUIVCUPVDUIVCFUMCDZUPABTZUMSRQUIUQUJASUAUBABUCABUDUEUKV
    EUNVAUFUJUIVCVEVDVFQULUMACUGRUH $.
    $( [5-Jan-2009] $)

  $( An open interval is included in a bound below interval.  (Contributed by
     FL, 26-Jan-2009.) $)
  oibbi1 $p |- ( A (,) B ) C_ ( A (,) +oo ) $=
    ( cxr wcel wa cioo co wss cle wbr simpl simpr pnfxr a1i pnfge adantl iooss2
    cpnf syl31anc c0 wn wceq ndmioo 0ss sseq1 mpbiri syl pm2.61i ) ACDZBCDZEZAB
    FGZARFGZHZUKUIUJRCDZBRIJZUNUIUJKUIUJLUOUKMNUJUPUIBOPABRQSUKUAULTUBZUNABUCUQ
    UNTUMHUMUDULTUMUEUFUGUH $.
    $( [26-Jan-2009] $)

  $( An open interval is included in a bound above interval.  (Contributed by
     FL, 26-Jan-2009.) $)
  oibbi2 $p |- ( A (,) B ) C_ ( -oo (,) B ) $=
    ( cxr wcel wa cioo co wss cle wbr mnfxr a1i simpl simpr mnfle adantr iooss1
    cmnf syl31anc c0 wn wceq ndmioo 0ss sseq1 mpbiri syl pm2.61i ) ACDZBCDZEZAB
    FGZRBFGZHZUKRCDZUIUJRAIJZUNUOUKKLUIUJMUIUJNUIUPUJAOPRABQSUKUAULTUBZUNABUCUQ
    UNTUMHUMUDULTUMUEUFUGUH $.
    $( [26-Jan-2009] $)

  $( Membership in an open interval of extended reals.  (Contributed by FL,
     7-Dec-2010.) $)
  nelioo5 $p |- ( ( A e. RR* /\ B e. RR* /\ C e. RR* )
           -> ( -. C e. ( A (,) B ) <-> ( C <_ A \/ B <_ C ) ) ) $=
    ( cxr wcel w3a cioo co wn clt wbr wa wo cle elioo5 notbid wb xrlenlt bicomd
    ianor a1i ancoms 3adant2 3adant1 orbi12d 3bitrd ) ADEZBDEZCDEZFZCABGHEZIACJ
    KZCBJKZLZIZULIZUMIZMZCANKZBCNKZMUJUKUNABCOPUOURQUJULUMTUAUJUPUSUQUTUGUIUPUS
    QUHUGUILUSUPUIUGUSUPQCARUBSUCUHUIUQUTQUGUHUILUTUQBCRSUDUEUF $.
    $( [7-Dec-2010] $)

