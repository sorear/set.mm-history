$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Extended Real numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c -e $. $( the negative of an extended real number. $)
  $c +e $. $( Addition of extended real numbers. $)
  $c xe $. $( Multiplication of extended real numbers. $)

  $( Extend class notation to include the negative of an extended number. $)
  clminex $a class -e A $.

  $( Extend class notation to include the negative of an extended number. $)
  claddex $a class +e $.

  $( Define the negative of an extended real number. $)
  df-nege $a |- -e A = if ( A = +oo , -oo , if ( A = -oo , +oo , -u A ) ) $.

  ${
    $d x y z $.
    $( Define addition over extended real numbers. $)
    df-adde $a |- +e = { <. <. x , y >. , z >. | ( x e. RR* /\ y e. RR*
     /\ z = if ( x = +oo ,
            if ( y = -oo , ~P RR* , +oo ) ,
         if ( x = -oo ,
            if ( y = +oo , ~P RR* , -oo ) ,
               if ( y = +oo , +oo ,
                 if ( y = -oo , -oo , ( x + y ) ) ) ) ) ) } $.
  $}

  $( Minus ` +oo ` .  Bourbaki TG IV.15.  (Contributed by FL, 26-Dec-2011.) $)
  mpinf $p |- -e +oo = -oo $=
    ( cpnf clminex wceq cmnf cneg cif df-nege eqid iftrue ax-mp eqtri ) ABAACZD
    ADCAAEFZFZDAGLNDCAHLDMIJK $.
    $( [26-Dec-2011] $)

  $( Minus ` -oo ` .  Bourbaki TG IV.15.  (Contributed by FL, 26-Dec-2011.) $)
  miminf $p |- -e -oo = +oo $=
    ( cmnf clminex cpnf wceq cneg cif df-nege wn wne pnfnemnf df-ne necom mpbir
    bitr3i iffalse eqid iftrue ax-mp syl6eq eqtri ) ABACDZAAADZCAEZFZFZCAGUAHZU
    ECDUFCAIZJUFACIUGACKACLNMUFUEUDCUAAUDOUBUDCDAPUBCUCQRSRT $.
    $( [26-Dec-2011] $)

  $( Minus a real number.  Bourbaki TG IV.15.  (Contributed by FL,
     26-Dec-2011.) $)
  mreal $p |- ( A e. RR -> -e A = -u A ) $=
    ( cr wcel cmnf wne cpnf clminex cneg wceq renemnf renepnf wa cif df-nege wn
    a1i df-ne iffalse sylbi adantl adantr 3eqtrd syl2anc ) ABCADEZAFEZAGZAHZIAJ
    AKUDUELZUFAFIZDADIZFUGMZMZUKUGUFULIUHANPUEULUKIZUDUEUIOUMAFQUIDUKRSTUDUKUGI
    ZUEUDUJOUNADQUJFUGRSUAUBUC $.
    $( [26-Dec-2011] $)

  ${
    mamb.1 $e |- A = B $.
    $( Equality of two extended numbers with ` -e ` in front of them.
       (Contributed by FL, 26-Dec-2011.) $)
    mamb $p |- -e A = -e B $=
      ( cpnf wceq cif wn wi ax-mp wa iffalse adantl a1i eqcomd adantr 3eqtrd ex
      cmnf pm2.43i iftrue cneg clminex eqeq1 notbid biimpd negeqi syl6bir ecase
      syl6bi sylan9eq mpancom df-nege 3eqtr4i ) ADEZRAREZDAUAZFZFZBDEZRBREZDBUA
      ZFZFZAUBBUBUNUSURVCEZUSGZUNGZVDABEZVFVEHCVGVFVEVGUNUSABDUCUDZUEIVEVFJZURU
      QVBVCVFURUQEZVEUNRUQKZLUQVBEZVIUOUTVLUOGZVLVGVMVMVLHZHCVGVMUTGZVNVGUOUTAB
      RUCUDZVOVMVLVOVMJZUQUPVAVBVMUQUPEZVOUODUPKZLUPVAEZVQABCUFZMVOVAVBEZVMVOVB
      VAUTDVAKNZOPQUIISVOVLVGVOVOVLHZHCVGVOVMWDVPVMVOVLVMVOJZUQUPVAVBVMVRVOVSOV
      TWEWAMVOWBVMWCLPQUGISUOUTUQDVBUODUPTUTVBDUTDVATNUJUHZMVEVBVCEZVFVEVCVBUSR
      VBKNZOPUKVEVDVGVEVEVDHZHCVGVEVFWIVHVFVEVDVFVEJZURUQVBVCVFVJVEVKOVLWJWFMVE
      WGVFWHLPQUGISUNUSURRVCUNRUQTUSVCRUSRVBTNUJUHAULBULUM $.
      $( [26-Dec-2011] $)
  $}

  ${
    nolimf.1 $e |- Y = U. L $.
    nolimf.2 $e |- J = ( topGen ` ran (,) ) $.
    ${
      $d F x $.  $d J x $.  $d L x $.  $d Y x $.
      $( A numerical function has at most one limit value.  (Contributed by FL,
         14-Nov-2010.) $)
      nolimf $p |- ( ( L e. Fil /\ F : Y --> RR )
       -> E* x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cha cr wf cv cflf co cfv wmo cioo cuni ctb crn eqeltri wceq
        ctg rehaus retopbas unitg ax-mp unieqi unirnioo 3eqtr4ri holimf mp3an2
        ) DHICJIEKBLAMBCDNOPIAQCRUAZUDPZJGUEUBABCDKEFUOSZUNSZCSKUNTIUPUQUCUFUNT
        UGUHCUOGUIUJUKULUM $.
        $( [14-Nov-2010] $)
    $}

    ${
      $d F x $.  $d J x $.  $d L x $.  $d Y x $.
      $( A numerical convergent function has one limit value.  (Contributed by
         FL, 14-Nov-2010.) $)
      nolimf2 $p |- ( ( L e. Fil /\ F : Y --> RR /\
       ( ( J fLimf L ) ` F ) =/= (/) )
         -> E! x x e. ( ( J fLimf L ) ` F ) ) $=
        ( cfil wcel cr wf cflf co cfv c0 wne w3a cv wmo weu 3adant3 wb 3ad2ant3
        nolimf n0moeu mpbid ) DHIZEJBKZBCDLMNZOPZQARUIIZASZUKATZUGUHULUJABCDEFG
        UDUAUJUGULUMUBUHAUIUEUCUF $.
        $( [14-Nov-2010] $)
    $}

  $}

  ${

    flimfnein.2 $e |- Y = U. L $.
    flimfnein.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfnein.4 $e |- J = ( topGen ` ran (,) ) $.

    ${
      $d F s $.  $d L s $.  $d N s $.  $d Y s $.
      $( A neighborhood of the limit value ` A ` of a convergent numerical
         function ` F ` contains the image of a filter element.  (Contributed
         by FL, 18-Nov-2010.) $)
      flimfnein $p |- ( ( ( L e. Fil /\ F : Y --> RR )
         /\ ( ( J fLimf L ) ` F ) =/= (/)
         /\ N e. ( ( nei ` J ) ` { A } ) ) -> E. s e. L ( F " s ) C_ N ) $=
        ( cfil wcel cr wf wa cha w3a cflf cfv cuni co c0 wne csn cnei cima wrex
        cv wss cioo crn ctg rehaus eqeltri a1i simpl simpr 3jca uniretop unieqi
        eqtr4i flimfneih syl3an1 ) DKLZFMBNZOZCPLZVDVEQBCDRUASUBUCEAUDCUESSLBGU
        HUFEUIGDUGVFVGVDVEVGVFCUJUKULSZPJUMUNUOVDVEUPVDVEUQURABCDEMFGMVHTCTUSCV
        HJUTVAHIVBVC $.
        $( [30-Jan-2014] $) $( [18-Nov-2010] $)
    $}

    ${
      $( The limit of a numerical convergent function belongs to ` RR ` .
         (Contributed by FL, 22-Nov-2010.) $)
      limnumrr $p |- ( ( L e. Fil /\ F : Y --> RR /\
       ( ( J fLimf L ) ` F ) =/= (/) )
         -> A e. RR ) $=
        ( cfil wcel cr wf cflf cfv ctop eqeltri a1i cha cuni syl31anc co c0 wne
        w3a cioo retop simp1 simp2 rehaus simp3 uniretop unieqi eqtr4i limvinlv
        crn ctg flimfelbas ) DIJZEKBLZBCDMUANZUBUCZUDZCOJZURUSAUTJZAKJVCVBCUEUO
        UPNZOHUFPQURUSVAUGZURUSVAUHZVBCRJZURUSVAVDVHVBCVERHUIPQVFVGURUSVAUJABCD
        KEKVESCSUKCVEHULUMZFGUNTABCDKEVIFUQT $.
        $( [30-Jan-2014] $) $( [22-Nov-2010] $)
    $}

  $}

  ${
    cinei.1 $e |- J = ( topGen ` ran (,) ) $.
    $( A centered interval is a neighborhood of its center.  (Contributed by
       FL, 18-Nov-2010.) $)
    cinei $p |- ( ( A e. RR /\ B e. RR+ )
      -> ( ( A - B ) (,) ( A + B ) ) e. ( ( nei ` J ) ` { A } ) ) $=
      ( cr wcel crp wa ctop cmin co caddc cioo csn cnei cfv crn ctg retop a1i
      eqeltri iooretop eleqtrri cbci opnneip syl3anc ) AEFBGFHZCIFZABJKZABLKZMK
      ZCFZAUKFUKANCOPPFUHUGCMQRPZIDSUATULUGUKUMCUIUJUBDUCTABUDACUKUEUF $.
      $( [18-Nov-2010] $)
  $}

  ${
    flimfneic.1 $e |- X = U. J $.
    flimfneic.2 $e |- Y = U. L $.
    flimfneic.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfneic.4 $e |- J = ( topGen ` ran (,) ) $.
    flimfneic.5 $e |- N = ( ( A - B ) (,) ( A + B ) ) $.
    $( A centered interval of the limit value ` A ` of a convergent numerical
       function ` F ` contains the image of a filter element.  (Contributed by
       FL, 18-Nov-2010.) $)
    flimfneic $p |- ( ( ( L e. Fil /\ F : Y --> X )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ B e. RR+ ) -> A e. ( ( J fLimf L ) ` F ) ) $=
      ( cfil wcel wf wa cfv cha cuni cflf co c0 wne crp w3a cioo crn ctg rehaus
      eqeltri a1i simp1l wceq wb unieqi eqtri feq3 ax-mp biimpi adantl 3ad2ant1
      simp2 eqcomi limvinlv syl31anc ) ENOZHGCPZQZCDEUAUBRZUCUDZBUEOZUFZDSOZVGH
      UGUHUIRZTZCPZVKAVJOVNVMDVOSLUJUKULVGVHVKVLUMVIVKVQVLVHVQVGVHVQGVPUNVHVQUO
      GDTZVPIDVOLUPZUQGVPHCURUSUTVAVBVIVKVLVCACDEVPHVRVPVSVDJKVEVF $.
      $( [18-Nov-2010] $)
  $}

  ${
    $d F s $.  $d L s $.  $d N s $.  $d Y s $.
    flimfneicn.2 $e |- Y = U. L $.
    flimfneicn.3 $e |- A = U. ( ( J fLimf L ) ` F ) $.
    flimfneicn.4 $e |- J = ( topGen ` ran (,) ) $.
    flimfneicn.5 $e |- N = ( ( A - B ) (,) ( A + B ) ) $.
    $( A centered interval of the limit value ` A ` of a convergent numerical
       function ` F ` contains the image of a filter element.  (Contributed by
       FL, 18-Nov-2010.) $)
    flimfneicn $p |- ( ( ( L e. Fil /\ F : Y --> RR )
       /\ ( ( J fLimf L ) ` F ) =/= (/)
       /\ B e. RR+ ) -> E. s e. L ( F " s ) C_ N ) $=
      ( wcel cr wa co cfv ctop cioo eqeltri cfil wf cflf c0 wne crp csn cnei cv
      cima wss wrex wi crn ctg retop a1i simpll simplr cha rehaus cuni uniretop
      unieqi eqtr4i limvinlv mp3anl1 flimfelbas syl31anc ex cmin caddc syl5eqel
      cinei syl6 3imp flimfnein syld3an3 ) EUAMZGNCUBZOZCDEUCPQZUDUEZBUFMZFAUGD
      UHQQZMZCHUIUJFUKHEULWAWCWDWFWAWCANMZWDWFUMWAWCWGWAWCOZDRMZVSVTAWBMZWGWIWH
      DSUNUOQZRKUPTUQVSVTWCURVSVTWCUSDUTMVSVTWCWJDWKUTKVATACDENGNWKVBDVBVCDWKKV
      DVEZIJVFVGACDENGWLIVHVIVJWGWDWFWGWDOFABVKPABVLPSPWELABDKVNVMVJVOVPACDEFGH
      IJKVQVR $.
      $( [18-Nov-2010] $)
  $}

  ${
    lvsovso.1 $e |- Y = U. F $.
    lvsovso.2 $e |- L1 = U. ( ( J fLimf F ) ` F1 ) $.
    lvsovso.3 $e |- L2 = U. ( ( J fLimf F ) ` F2 ) $.
    lvsovso.4 $e |- J = ( topGen ` ran (,) ) $.

    ${
      $d F a r s x $.  $d F1 a r s x $.  $d F2 a r s x $.  $d J r s x $.
      $d L1 r s x $.  $d L2 r s x $.  $d Y r s x $.
      $( If the limit values of two convergent numerical functions are strictly
         ordered, the values of the functions are strictly ordered for some
         element of the filter.  Bourbaki TG IV.18 prop. 2.  (Contributed by
         FL, 6-Dec-2010.) $)
      lvsovso $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             L1 < L2 ) )
         -> E. a e. F A. x e. a ( F1 ` x ) < ( F2 ` x ) ) $=
        ( co wa wcel cr w3a wi 3syl vr vs cv cima cmin cabs cfv cdiv caddc cioo
        c2 wss wrex cfil cflf wne clt wbr wral crp simpl1 simpl2 jca simpr1 cc0
        wf c0 limnumrr recnd 3expia com12 3ad2ant2 3adant2 imp 3ad2ant1 3adant3
        cc subcl syl2anc abscl rehalfcl resubcl simpr3 posdif gt0ne0 absgt0 syl
        wb mpbid halfpos2 elrp sylanbrc simpl3 eqid flimfneicn anim12i syl33anc
        simpr2 cin filint inss1 inss2 imass2 mp2an sstr2 im2anan9 ad2antlr cuni
        ax-mp elunii syl6eleqr expcom ffvelrn adantl adantr 3jca wfun ffun wceq
        cdm fdm eqcomd sseq2d biimpcd 3exp funfvima2 syl6 3ad2ant3 recn syl3anc
        impcom ancoms addcl subadd simpl ssel2 eliooord an4s ex rexlimiva sseq2
        elssuni biimpd eqcoms 3impia imp31 halfcl add12 2halves cle 0re biimp3a
        3imp jctil ltle absid eqtr2d eqtrd mpbird simpr breq2 anbi1d imbi2d mpd
        sylc syl5ibr axlttrn ralrimiv raleq rcla4ev pm2.43i syl7 3expib anabsi5
        com14 com23 com3l mpcom ) FUAUCZUDZHIHUENZUFUGZUKUHNZUENZHUWCUINZUJNZUL
        ZUABUMZGUBUCZUDZIUWCUENZIUWCUINZUJNZULZUBBUMZOZBUNPZDQFVFZDQGVFZRZFCBUO
        NZUGVGUPZGUXAUGVGUPZHIUQURZRZOZAUCZFUGZUXGGUGZUQURZAEUCZUSZEBUMZUXFUWQU
        WROZUXBUWCUTPZUWQUWSOZUXCUXOUWPUXFUWQUWRUWQUWRUWSUXEVAZUWQUWRUWSUXEVBZV
        CUWTUXBUXCUXDVDUXFUWCQPZVEUWCUQURZUXOUXFUWAVQPZUWBQPZUXSUXFIVQPZHVQPZUY
        AUWTUXEUYCUWQUWSUXEUYCSUWRUXEUXPUYCUXCUXBUXPUYCSUXDUXPUXCUYCUWQUWSUXCUY
        CUWQUWSUXCRIIGCBDJLMVHZVIVJVKVLVKVMVNUWTUXEUYDUWQUWRUXEUYDSUWSUXEUXNUYD
        UXBUXCUXNUYDSUXDUXNUXBUYDUWQUWRUXBUYDUWQUWRUXBRHHFCBDJKMVHZVIVJVKVOVKVP
        VNIHVRZVSZUWAVTZUWBWAZTZUXFVEUWBUQURZUXTUXFUWAVEUPZUYLUXFUWAQPZVEUWAUQU
        RZUYMUXFIQPZHQPZUYNUWTUXEUYPUWQUWSUXEUYPSUWRUXEUXPUYPUXCUXBUXPUYPSUXDUX
        PUXCUYPUWQUWSUXCUYPUYEVJVKVLVKVMVNZUWTUXEUYQUWQUWRUXEUYQSUWSUXEUXNUYQUX
        BUXCUXNUYQSUXDUXNUXBUYQUWQUWRUXBUYQUYFVJVKVOVKVPVNZIHWBZVSUXFUXDUYOUWTU
        XBUXCUXDWCZUXFUYQUYPUXDUYOWHUYSUYRHIWDZVSWIUWAWEVSUXFUYAUYMUYLWHUYHUWAW
        FWGWIUXFUYAUYBUYLUXTWHUYHUYIUWBWJTWIUWCWKWLZUXFUWQUWSUXQUWQUWRUWSUXEWMZ
        VCUWTUXBUXCUXDWRVUCUXNUXBUXORUWHUXPUXCUXORUWOHUWCFCBUWFDUAJKMUWFWNWOIUW
        CGCBUWMDUBJLMUWMWNWOWPWQUWHUWOUXFUXMSZUWGUWOVUESUABUWOUVSBPZUWGOZVUEUWN
        VUGVUESUBBVUGUWIBPZUWNOVUEVUFVUHUWGUWNVUEVUFVUHOZUWGUWNOZVUEUXFVUIVUJUX
        MUWTUXEVUIVUJUXMSZSZUWQUWRUXFVULSUWSUWQVUIUXFVUKUWQVUFVUHUXFVUKSZUWQVUF
        VUHRUVSUWIWSZBPZVUMUVSUWIBWTVUJFVUNUDZUWFULZGVUNUDZUWMULZOZVUOUXFUXMVUP
        UVTULZVURUWJULZOZVUJVUTSVUNUVSULZVUNUWIULZVVCUVSUWIXAUVSUWIXBVVDVVAVVEV
        VBVUNUVSFXCVUNUWIGXCWPXDVVAUWGVUQVVBUWNVUSVUPUVTUWFXEVURUWJUWMXEXFXIVUO
        UXFVUTUXMSSVUTVUOUXFVUOUXMVUTVUOUXFVUOUXMSZSVUTVUOOZUXFUXJAVUNUSZVVFVVG
        UXFVVHVVGUXFOZUXJAVUNVVIUXGVUNPZUXHQPZUWKQPZUXIQPZRZUXHUWKUQURZUWKUXIUQ
        URZOZOZUXJVVIVVJVVRVVIVVJOZVVNVVQVVSVVKVVLVVMVVSUWRUXGDPZVVKUXFUWRVVGVV
        JUXRXGVVIVVJVVTVUOVVJVVTSVUTUXFVVJVUOVVTVVJVUOOUXGBXHZDUXGVUNBXJJXKXLXG
        VNZDQUXGFXMVSVVIVVLVVJVVIUYPUXSVVLUXFUYPVVGUYRXNUXFUXSVVGUYKXNIUWCWBVSX
        OVVSUWSVVTVVMUXFUWSVVGVVJVUDXGVWBDQUXGGXMVSXPVVSUXHVUPPZUXIVURPZOZVVQVV
        GUXFVVJVWEVUOUXFVVJVWESZSVUTUXFVUOVWFUWTVUOVWFSUXEUWTVUOVVJVWEUWTVUOVVJ
        RVWCVWDUWTVUOVVJVWCUWTVUOFXQZVUNFXTZULZOZVVJVWCSUWQUWRVUOVWJSZUWSUWRUWQ
        VWKUWRUWQVUOVWJUWRUWQVUORVWGVWIUWRUWQVWGVUODQFXRVOUWRVUOVWIUWQVUOUWRVWI
        VUOVUNVWAULZVUNDULZUWRVWISVUNBUUBZDVWAXSVWLVWMSZJVWOVWADVWADXSVWLVWMVWA
        DVUNUUAUUCUUDXIZUWRVWMVWIUWRDVWHVUNUWRVWHDDQFYAYBYCYDTYKVMVCYEYKVPVUNUX
        GFYFYGUUMUWTVUOVVJVWDUWTVUOOGXQZVUNGXTZULZVVJVWDSUWTVWQVUOUWSUWQVWQUWRD
        QGXRYHXOUWTVUOVWSUWSUWQVUOVWSSUWRVUOUWSVWSVUOVWLVWMUWSVWSSVWNVWPUWSVWMV
        WSUWSDVWRVUNUWSVWRDDQGYAYBYCYDTVKYHVNVUNUXGGYFVSUUEVCYEXOVKXNUUFVVIVWEV
        VQSZVVJVVGUXFVWTVUTUXFVWTSVUOUXFVUTVWTUXFUWKUWEXSZVUTVWTSUXFUYQUYPUXDVX
        AUYSUYRVUAUYQUYPUXDRZVXAUWCUWEUINZIXSZVXBVXCHUWCUWCUINZUINZIVXBUWCVQPZU
        YDVXGVXCVXFXSUYQUYPVXGUXDUYQUYPOZUYBUWBVQPZVXGVXHUYCUYDOZUYAUYBUYPUYQVX
        JUYPUYCUYQUYDIYIZHYIZWPYLZUYGUYITUWBYIZUWBUUGZTVPZUYQUYPUYDUXDVXLVOVXPU
        WCHUWCUUHYJVXBUWAVXEXSZVXFIXSZVXBVXEUWBUWAVXBVXIVXEUWBXSUYQUYPVXIUXDVXH
        UYAUYBVXIVXHVXJUYAVXMUYGWGZUYIVXNTVPUWBUUIWGVXBUYNVEUWAUUJURZUWBUWAXSUY
        QUYPUYNUXDUYPUYQUYNUYTYLZVPVXBVEQPZUYNOZUYOVXTUYQUYPVYCUXDVXHUYNVYBVYAU
        UKUUNVPUYQUYPUXDUYOVUBUULVEUWAUUOUVEUWAUUPVSUUQVXBUYCUYDVXEVQPZRZVXQVXR
        WHUYQUYPVYEUXDVXHUYCUYDVYDUYPUYCUYQVXKXNZUYQUYDUYPVXLXOVXHUXSVXGVXGOVYD
        VXHUYAUYBUXSVXSUYIUYJTZUXSVXGVXGUWCYIZVYHVCUWCUWCYMTXPVPIHVXEYNWGWIUURV
        XBUYCVXGUWEVQPZRZVXAVXDWHUYQUYPVYJUXDVXHUYCVXGVYIVYFVXHUYBVXIVXGVXHUYNU
        YAUYBVYAUWAYIUYITVXNVXOTVXHUYQUXSOUYDVXGOVYIVXHUYQUXSUYQUYPYOVYGVCUYQUY
        DUXSVXGVXLVYHWPHUWCYMTXPVPIUWCUWEYNWGUUSYJVUTVWTVXAVWEUXHUWEUQURZVVPOZS
        VUTVWEVYLVUQVWCVUSVWDVYLVUQVWCOZVYKVUSVWDOZVVPVYMUXHUWFPUWDUXHUQURZVYKO
        VYKVUPUWFUXHYPUXHUWDUWEYQVYOVYKUUTTVYNUXIUWMPVVPUXIUWLUQURZOVVPVURUWMUX
        IYPUXIUWKUWLYQVVPVYPYOTWPYRYSVXAVVQVYLVWEVXAVVOVYKVVPUWKUWEUXHUQUVAUVBU
        VCUVFWGVKXOVNXOUVDVCYSVVNVVQUXJUXHUWKUXIUVGVNYGUVHYSVUOVVHUXMUXLVVHEVUN
        BUXJAUXKVUNUVIUVJXLYGYSUVOUVKUVLWGUVMUVPVOUVNUVQVNYRXLYTVKYTVNUVR $.
        $( [6-Dec-2010] $)
    $}

    ${
      $d F a x $.  $d F1 a x $.  $d F2 a x $.  $d J a x $.  $d L1 x $.
      $d L2 x $.  $d Y a x $.
      $( Condition on the elements of the filter so that the limits are weakly
         ordered.  Bourbaki TG IV.18 prop. 1.  (Contributed by FL,
         30-Dec-2010.) $)
      lvsovso2 $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             A. a e. F E. x e. a ( F1 ` x ) <_ ( F2 ` x ) ) )
         -> L1 <_ L2 ) $=
        ( cfv wbr w3a wcel cr wi wn cflf co c0 wne cv cle wrex wral cfil wf clt
        lvsovso ex 3com23 3expd com13 3imp wb simp31 simp33 simp2 3jca limnumrr
        rexr 3syl simp32 simp1 xrltnle syl2anc bicomd rexnal wa ralnex ad2antrr
        cxr cuni elunii expcom a1i imp31 syl6eleqr jca ffvelrn ralbidva syl5bbr
        rexbidva 3imtr4d con4d 3exp com34 impcom ) FCBUAUBZNUCUDZGWLNUCUDZAUEZF
        NZWOGNZUFOZAEUEZUGZEBUHZPBUIQZDRFUJZDRGUJZPZHIUFOZWMWNXAXEXFSWMWNXEXAXF
        WMWNXEXAXFSWMWNXEPZXFXAXGIHUKOZWQWPUKOZAWSUHZEBUGZXFTZXATZWMWNXEXHXKSZX
        EWNWMXNXEWNWMXHXKXBXDXCWNWMXHPZXKSXBXDXCPXOXKABCDEGFIHJLKMULUMUNUOUPUQX
        GXHXLXGIVOQZHVOQZXHXLURXGXBXDWNPIRQXPXGXBXDWNWMWNXBXCXDUSZWMWNXBXCXDUTZ
        WMWNXEVAVBIGCBDJLMVCIVDVEXGXBXCWMPHRQXQXGXBXCWMXRWMWNXBXCXDVFZWMWNXEVGV
        BHFCBDJKMVCHVDVEIHVHVIVJXMWTTZEBUGXGXKWTEBVKXGYAXJEBYAWRTZAWSUHXGWSBQZV
        LZXJWRAWSVMYDYBXIAWSYDWOWSQZVLZXIYBYFWQVOQZWPVOQZXIYBURYFXDWODQZVLWQRQY
        GYFXDYIXGXDYCYEXSVNYFWOBVPZDXGYCYEWOYJQZYCYEYKSSXGYEYCYKWOWSBVQVRVSVTJW
        AZWBDRWOGWCWQVDVEYFXCYIVLWPRQYHYFXCYIXGXCYCYEXTVNYLWBDRWOFWCWPVDVEWQWPV
        HVIVJWDWEWFWEWGWHWIWJUQWK $.
        $( [30-Dec-2010] $)
    $}

    ${
      $d F a x $.  $d F1 a x $.  $d F2 a x $.  $d J a x $.  $d L1 x $.
      $d L2 x $.  $d Y a x $.
      $( Condition on the values of two numerical functions so that their
         limits are weakly ordered.  Bourbaki TG IV.18 th. 1.  (Contributed by
         FL, 18-Jul-2011.) $)
      lvsovso3 $p |- ( ( ( F e. Fil /\ F1 : Y --> RR /\ F2 : Y --> RR ) /\
           ( ( ( J fLimf F ) ` F1 ) =/= (/) /\
             ( ( J fLimf F ) ` F2 ) =/= (/) /\
             A. x e. Y ( F1 ` x ) <_ ( F2 ` x ) ) )
         -> L1 <_ L2 ) $=
        ( va wcel w3a cfv c0 wne wral wi cfil cr wf cflf co cle wbr wrex simpr1
        cv wa simpr2 cuni wss elssuni eqcomi sseq2i elfilnemp ssralv r19.2z a1i
        ex com13 syl6 com4l 3ad2ant3 syl5com com23 3ad2ant1 com3l pm2.43i com3r
        imp sylbi mpcom com12 ralrimiv 3jca lvsovso2 syldan ) BUANZDUBEUCZDUBFU
        CZOZECBUDUEZPQRZFWEPQRZAUJZEPWHFPUFUGZADSZOZWFWGWIAMUJZUHZMBSZOGHUFUGWD
        WKUKZWFWGWNWDWFWGWJUIWDWFWGWJULWOWMMBWLBNZWOWMWLBUMZUNZWPWOWMTZWLBUOWRW
        LDUNZWPWSTWQDWLDWQIUPUQWPWOWTWMWPWOWTWMTZTWOWPWPXAWDWKWPWPXATZTZWAWBWKX
        CTWCWAWPWKXBWAWPWKXBTWAWPUKWLQRZWKXBWLBURWJWFXDXBTWGWTWJXDWPWMWTWJWIAWL
        SZXDWPWMTTWIAWLDUSWPXDXEWMXDXEWMTTWPXDXEWMWIAWLUTVBVAVCVDVEVFVGVBVHVIVM
        VJVKVLVNVOVPVQVRABCDMEFGHIJKLVSVT $.
        $( [18-Jul-2011] $)
    $}
  $}

  ${
    $d A x y z $.  $d C x y z $.  $d F w x $.  $d F x y z $.
    $( The supremum of a numerical function ` F ` is greater or equal to every
       element of ` ( F `` A ) ` .  Bourbaki TG IV.20.  (Contributed by FL,
       25-Dec-2011.) $)
    supnuf $p |- ( ( F : A --> RR* /\ A e. _V /\ C e. A ) -> ( F ` C ) <_ ( <_
        supw ran F ) ) $=
      ( vx vy vz cxr cvv wcel cle wbr cv wral wi wa syl 3ad2ant1 wrex syl2anc
      wn vw wf w3a cfv crn cspw co cps ctsr lteqttos cuni cxp ccnv cun wss eqid
      istoset id adantr sylbi ax-mp a1i cdm wfun wceq fdm biimpd eqcoms 3adant3
      eleq1 imp ffun funrnex sylc frn xrex clt xrsupss wb sselda simplr xrlenlt
      ssex ralbidva con34b simpr xrltnle bicomd sseld ad2antrr rexbidva syl5bbr
      rexnal imbi12d syl5bb anbi12d mpbird domleqt eqcomi syl3anc supdef eqcomd
      biid spwcl 3jca eleq2d biimpa 3adant2 fvelrn breq1 rcla4v com12 pm2.43i
      3syl ) AGCUBZAHIZBAIZUCZBCUDZJCUEZUFUGZJKZXRJUHIZXTHIZYAGIZUCDLZYAJKZDXTM
      ZUALYFJKUAXTMYAYFJKNDGMZOXRYBNZXRYCYDYEYCXRJUIIZYCUJYKYCJUKUKZYLULJJUMUNU
      OZOYCJYLYLUPUQYCYCYMYCURUSUTVAVBZXRCVCZHIZCVDZYDXOXPYPXQXOXPYPXOYOAVEXPYP
      NZAGCVFZYRAYOAYOVEXPYPAYOHVJVGVHPVKVIXOXPYQXQAGCVLQZHCVMVNXRYCYDELZYFJKZE
      XTMZFLZUUAJKZFXTMZYFUUAJKZNZEGMZOZDGRZYEYNXOXPYDXQXOXTGUOZYDAGCVOZXTGVPWC
      PQXRUUKYFUUAVQKTZEXTMZUUAYFVQKZUUAUUDVQKZFXTRZNZEGMZOZDGRZXRUULUVBXOXPUUL
      XQUUMQZDEFXTVRPXRUUJUVADGXRYFGIZOZUUCUUOUUIUUTUVEUUBUUNEXTUVEUUAXTIZOUUAG
      IZUVDUUBUUNVSUVEXTGUUAXRUULUVDUVCUSVTXRUVDUVFWAUUAYFWBSWDUVEUUHUUSEGUUHUU
      GTZUUFTZNUVEUVGOZUUSUUFUUGWEUVJUVHUUPUVIUURUVJUUPUVHUVJUVGUVDUUPUVHVSUVEU
      VGWFXRUVDUVGWAUUAYFWGSWHUVIUUETZFXTRUVJUURUUEFXTWMUVJUVKUUQFXTUVJUUDXTIZO
      ZUUQUVKUVMUVGUUDGIZUUQUVKVSUVEUVGUVLWAUVJUVLUVNXRUVLUVNNZUVDUVGXOXPUVOXQX
      OXTGUUDUUMWIQWJVKUUAUUDWGSWHWKWLWNWOWDWPWKWQUUJDEFXTJHGJVCGWRWSZUUJXCXDWT
      XEDUAXTJHGUVPXAYHYJYIXRYHYBXRXSXTIZYHYBNXRYQBYOIZUVQYTXOXQUVRXPXOXQUVRXOA
      YOBXOYOAYSXBXFXGXHBCXISYGYBDXSXTYFXSYAJXJXKPXLUSXNXM $.
      $( [25-Dec-2011] $)
  $}

  ${
    $d A x $.  $d C x $.  $d V x $.
    supnufb.1 $e |- F = ( x e. A |-> U ) $.
    supnufb.2 $e |- ( x = C -> U = V ) $.
    $( The supremum of a numerical function ` F ` is greater or equal to every
       element of ` ( F `` A ) ` .  Bourbaki TG IV.20.  (Contributed by FL,
       25-Dec-2011.) $)
    supnufb $p |- ( ( A. x e. A U e. RR* /\ A e. M
       /\ ( C e. A /\ V e. N ) ) -> V <_ ( <_ supw ran F ) ) $=
      ( cxr wcel wral wa w3a cfv cle crn cspw co wceq fvmptg 3ad2ant3 wf biimpi
      cvv wbr fmpt 3ad2ant1 elex 3ad2ant2 simp3l supnuf syl3anc eqbrtrrd ) DKLA
      BMZBFLZCBLZHGLZNZOZCEPZHQERSTZQUTUPVBHUAUQACDHBGEJIUBUCVABKEUDZBUFLZURVBV
      CQUGUPUQVDUTUPVDABKDEIUHUEUIUQUPVEUTBFUJUKUPUQURUSULBCEUMUNUO $.
      $( [25-Dec-2011] $)
  $}

  ${
    $d A x y z $.
    $( Two ways to express the supremum of a set of extended reals.
       (Contributed by FL, 25-Dec-2011.) $)
    supexr $p |- ( A C_ RR* -> ( <_ supw A ) = sup ( A , RR* , < ) ) $=
      ( vy vx vz cxr wss cv cle wbr wral wi wa crab cuni clt wceq wcel wrex wn
      wb crio cspw csup wreu cps ctsr lteqttos cxp ccnv cun eqid istoset adantr
      co id sylbi ax-mp xrsupss ssel xrlenlt ex syl6 com23 imp31 con34b xrltnle
      ralbidva ancoms bicomd adantll rexnal syl5bbr imbi12d syl5bb anbi12d biid
      rexbidva mpbird sylancr riotauni syl cvv xrex ssex fldleqt eqcomi spwval2
      spweu rabbidva unieqd df-sup syl6reqr 3eqtr4d ) AEFZBGZCGZHIZBAJZDGZWOHIZ
      DAJZWPWOHIZKZBEJZLZCEUAZXECEMZNZHAUBUNZAEOUCZWNXECEUDZXFXHPWNHUEQZXECERZX
      KHUFQZXLUGXNXLHNNZXOUHHHUIUJFZLXLHXOXOUKULXLXLXPXLUOUMUPUQZWNXMWPWOOISZBA
      JZWOWPOIZWOWSOIZDARZKZBEJZLZCERCBDAURWNXEYECEWNWPEQZLZWRXSXDYDYGWQXRBAWNY
      FWOAQZWQXRTZWNYHYFYIWNYHWOEQZYFYIKAEWOUSYJYFYIWOWPUTVAVBVCVDVGYGXCYCBEXCX
      BSZXASZKYGYJLZYCXAXBVEYMYKXTYLYBYFYJYKXTTWNYFYJLXTYKYJYFXTYKTWOWPVFVHVIVJ
      YLWTSZDARYMYBWTDAVKYMYNYADAYGYJWSAQZYNYATZWNYJYOYPKKYFWNYOYJYPWNYOWSEQZYJ
      YPKAEWSUSYQYJYPYQYJLYAYNYJYQYAYNTWOWSVFVHVIVAVBVCUMVDVQVLVMVNVGVOZVQVRXEC
      BDAHEXEVPWHVSXECEVTWAWNXLAWBQXIXFPXQAEWCWDCBDAHWBEXOEWEWFWGVSWNXHYECEMZNX
      JWNXGYSWNXEYECEYRWIWJCBDAEOWKWLWM $.
      $( [20-Nov-2013] $) $( [25-Dec-2011] $)
  $}

  $c sup_ $. $( The supremum of an indexed class. $)
  $( Extend class notation to include the supremum of the class B. $)
  clsupp $a class sup_ x e. A B $.

  $c inf_ $. $( The infimum of an indexed class. $)
  $( Extend class notation to include the infimum of the class B. $)
  clinfp $a class inf_ x e. A B $.

  $( Definition of the supremum of an indexed class of extended reals. $)
  df-supp $a |- sup_ x e. A B = ( <_ supw ( ( x e. A |-> B ) " A ) ) $.

  $( Definition of the infimum of an indexed class of extended reals. $)
  df-infp $a |- inf_ x e. A B = ( <_ infw ( ( x e. A |-> B ) " A ) ) $.

  $( The supremum of a set of extended reals always exists. see supxrcl $)

  ${
    $d A x y $.  $d B y $.
    supbrr.1 $e |- B e. C $.
    $( The supremum of a set of extended reals always exists.  (Contributed by
       FL, 16-Apr-2012.) $)
    supbrr $p |- ( A. x e. A B e. RR* -> sup_ x e. A B e. RR* ) $=
      ( vy cxr wcel wral clsupp cle cmpt cima cspw co df-supp clt wss wceq syl
      csup crn df-mpt elexi rnssopab wfn eqid fnmpt eqcomd sseq1d biimpd syl5bi
      fnima pm2.43i supexr supxrcl eqeltrd syl5eqel ) CGHABIZABCJKABCLZBMZNOZGA
      BCPUSVBVAGQUAZGUSVAGRZVBVCSUSVDUSUTUBZGRZUSVDAFBGCUTAFBCUCCDEUDUEUSVFVDUS
      VEVAGUSVAVEUSUTBUFVAVESABCUTGUTUGUHBUTUMTUIUJUKULUNZVAUOTUSVDVCGHVGVAUPTU
      QUR $.
      $( [16-Apr-2012] $)
  $}

  $c Frf $. $( Frechet' s filter. $)

  $( Extends class notation with Frechet's filter. $)
  cfrf $a class Frf $.

  ${
    $d x b $.
    $( Frechet's filter.  Used to define the limit of a sequence. $)
    df-frf $a |- Frf = { x | E. b ( b C_ NN /\ b e. Fin /\ x = ( NN \ b ) ) }
        $.
  $}

  ${
    $d x y A $.
    $( Membership to the set of closed intervals.  (Contributed by FL,
       29-May-2014.) $)
    bsi2 $p |- ( A e. ran [,] <->
      E. x e. RR* E. y e. RR* A = ( x [,] y ) ) $=
      ( cxr cxp cpw cicc wf wfn crn wcel cv co wceq wrex iccf ffn ovelrn mp2b
      wb ) DDEZDFZGHGUAICGJKCALBLGMNBDOADOTPUAUBGQABDDCGRS $.
      $( [29-May-2014] $)
  $}

  ${
    $d x y z $.
    $( The set of closed-below, open-above intervals of extended reals maps to
       subsets of extended reals.  (Contributed by FL, 29-May-2014.) $)
    icof $p |- [,) : ( RR* X. RR* ) --> ~P RR* $=
      ( vx vy vz cle clt cico df-ico ixxf ) ABCDEFABCGH $.
      $( [29-May-2014] $)
  $}

  ${
    $d x y A $.
    $( Membership to the set of closed-above, open-below intervals.
       (Contributed by FL, 29-May-2014.) $)
    bsi3 $p |- ( A e. ran [,) <->
      E. x e. RR* E. y e. RR* A = ( x [,) y ) ) $=
      ( cxr cxp cpw cico wf wfn crn wcel cv co wceq wrex icof ffn ovelrn mp2b
      wb ) DDEZDFZGHGUAICGJKCALBLGMNBDOADOTPUAUBGQABDDCGRS $.
      $( [29-May-2014] $)
  $}

  ${
    $d x y z $.
    $( The set of closed-below, open-above intervals of extended reals maps to
       subsets of extended reals.  (Contributed by FL, 29-May-2014.) $)
    iocf $p |- (,] : ( RR* X. RR* ) --> ~P RR* $=
      ( vx vy vz clt cle cioc df-ioc ixxf ) ABCDEFABCGH $.
      $( [29-May-2014] $)
  $}

  ${
    $d x y A $.
    $( Membership to the set of open-below, closed-above intervals.
       (Contributed by FL, 29-May-2014.) $)
    bsi4 $p |- ( A e. ran (,] <->
      E. x e. RR* E. y e. RR* A = ( x (,] y ) ) $=
      ( cxr cxp cpw cioc wf wfn crn wcel cv co wceq wrex iocf ffn ovelrn mp2b
      wb ) DDEZDFZGHGUAICGJKCALBLGMNBDOADOTPUAUBGQABDDCGRS $.
      $( [29-May-2014] $)
  $}

