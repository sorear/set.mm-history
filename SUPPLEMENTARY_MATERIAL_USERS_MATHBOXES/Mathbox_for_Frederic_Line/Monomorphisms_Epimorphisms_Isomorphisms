$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Monomorphisms, Epimorphisms, Isomorphisms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Epic $. $( The class of all epimorphisms. $)
  $c Mono $. $( The class of all monomorphisms. $)
  $c Iso $. $( The class of all isomorphisms. $)

  $( Extend class notation with the class of all epimorphisms. $)
  cepi $a class Epic $.

  $( Extend class notation with the class of all monomorphisms. $)
  cmon $a class Mono $.

  $( Extend class notation with the class of all isomorphisms. $)
  ciso $a class Iso $.

  ${
    $d x y f g h $.
    $( Function returning the monomorphisms of the category ` x ` .  JFM CAT_1
       def. 10. $)
    df-mon $a |- Mono = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. dom ( dom_ ` x ) /\
      A. g e. dom ( dom_ ` x ) A. h e. dom ( dom_ ` x )
        ( ( ( ( dom_ ` x ) ` g ) = ( ( dom_ ` x ) ` h ) /\
            ( ( cod_ ` x ) ` g ) = ( ( dom_ ` x ) ` f ) /\
            ( ( cod_ ` x ) ` h ) = ( ( dom_ ` x ) ` f ) ) ->
              ( ( f ( o_ ` x ) g ) = ( f ( o_ ` x ) h ) -> g = h ) ) ) } ) } $.
  $}

  ${
    $d x y f g h $.
    $( Function returning the epimorphisms of the category ` x ` .  JFM CAT_1
       def. 11. $)
    df-epi $a |- Epic = { <. x , y >. |
    ( x e. Cat /\ y = { f | ( f e. dom ( dom_ ` x ) /\
      A. g e. dom ( dom_ ` x ) A. h e. dom ( dom_ ` x )
        ( ( ( ( cod_ ` x ) ` g ) = ( ( cod_ ` x ) ` h ) /\
            ( ( dom_ ` x ) ` g ) = ( ( cod_ ` x ) ` f ) /\
            ( ( dom_ ` x ) ` h ) = ( ( cod_ ` x ) ` f ) ) ->
              ( ( g ( o_ ` x ) f ) = ( h ( o_ ` x ) f ) -> g = h ) ) ) } ) } $.
  $}

  ${
    $d x y f g $.
    $( Function returning the isomorphisms of the category ` x ` .  The Joy of
       Cats p. 28. $)
    df-isoc $a |- Iso = { <. x , y >. | ( x e. Cat /\
    y = { f e. dom ( dom_ ` x ) | E. g e. dom ( dom_ ` x ) (
     ( ( dom_ ` x ) ` g ) = ( ( cod_ ` x ) ` f ) /\
     ( ( cod_ ` x ) ` g ) = ( ( dom_ ` x ) ` f ) /\
     ( ( f ( o_ ` x ) g ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` g ) ) /\
     ( g ( o_ ` x ) f ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` f ) ) ) ) } ) } $.
  $}


  ${
    $d C x y $.  $d D x y $.  $d M f g h x y $.  $d R x y $.  $d T f g h x y $.
    ismona.1 $e |- M = dom ( dom_ ` T ) $.
    ismona.2 $e |- D = ( dom_ ` T ) $.
    ismona.3 $e |- C = ( cod_ ` T ) $.
    ismona.4 $e |- R = ( o_ ` T ) $.
    $( Monomorphisms of a category.  (Contributed by FL, 5-Dec-2007.) $)
    ismona $p |- ( T e. Cat -> ( Mono ` T ) = { f | ( f e. M /\
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` f ) /\
            ( C ` h ) = ( D ` f ) ) ->
              ( ( f R g ) = ( f R h ) -> g = h ) ) ) } ) $=
      ( cv cfv wceq co wral fveq1d fveq1i syl6eq vx vy cdom_ cdm wcel ccod_ w3a
      co_ wi wa cab ccat cmon fveq2 dmeqd syl6eqr eleq2d raleqdv ralbidv eqcomi
      eqeq12d 3anbi123d oveqd oveq eqcoms ax-mp eqeq12i syl6bb imbi12d 2ralbidv
      imbi1d 3bitrd anbi12d abbidv df-mon cvv fvex dmex eqeltri zfausab fvopab4
      ) UAUBDEMZUAMZUCNZUDZUEZFMZWDNZGMZWDNZOZWGWCUFNZNZWBWDNZOZWIWLNZWNOZUGZWB
      WGWCUHNZPZWBWIWSPZOZWGWIOZUIZUIZGWEQZFWEQZUJZEUKWBHUEZWGBNZWIBNZOZWGANZWB
      BNZOZWIANZXNOZUGZWBWGCPZWBWICPZOZXCUIZUIZGHQFHQZUJZEUKULUMWCDOZXHYEEYFWFX
      IXGYDYFWEHWBYFWEDUCNZUDZHYFWDYGWCDUCUNZUOIUPZUQYFXGXEGHQZFWEQYKFHQYDYFXFY
      KFWEYFXEGWEHYJURUSYFYKFWEHYJURYFXEYCFGHHYFWRXRXDYBYFWKXLWOXOWQXQYFWHXJWJX
      KYFWHWGYGNXJYFWGWDYGYIRWGYGBBYGJUTZSTYFWJWIYGNXKYFWIWDYGYIRWIYGBYLSTVAYFW
      MXMWNXNYFWMWGDUFNZNXMYFWGWLYMWCDUFUNZRWGYMAAYMKUTZSTYFWNWBYGNXNYFWBWDYGYI
      RWBYGBYLSTZVAYFWPXPWNXNYFWPWIYMNXPYFWIWLYMYNRWIYMAYOSTYPVAVBYFXBYAXCYFXBW
      BWGDUHNZPZWBWIYQPZOYAYFWTYRXAYSYFWSYQWBWGWCDUHUNZVCYFWSYQWBWIYTVCVAYRXSYS
      XTCYQOZYRXSOZLUUBYQCWBWGYQCVDVEVFUUAYSXTOZLUUCYQCWBWIYQCVDVEVFVGVHVKVIVJV
      LVMVNUAUBEFGVOYDEHHYHVPIYGDUCVQVRVSVTWA $.
      $( [5-Dec-2007] $)
  $}

  ${
    $d C f $.  $d D f $.  $d F f g h $.  $d M f g h $.  $d R f $.
    $d T f g h $.
    ismonb.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb.2 $e |- D = ( dom_ ` T ) $.
    ismonb.3 $e |- C = ( cod_ ` T ) $.
    ismonb.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism".  (Contributed by FL, 2-Jan-2008.) $)
    ismonb $p |- ( ( T e. Cat /\ F e. A ) -> ( F e. ( Mono ` T )
      <-> ( F e. M /\ A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) ) ) $=
      ( vf wcel cfv wceq co wi wral ccat wa cmon cv w3a ismona adantr eleq2d wb
      eleq1 fveq2 eqeq2d 3anbi23d oveq1 eqeq12d imbi1d imbi12d 2ralbidv anbi12d
      cab elabg adantl bitrd ) EUAOZHAOZUBZHEUCPZOHNUDZIOZFUDZCPGUDZCPQZVJBPZVH
      CPZQZVKBPZVNQZUEZVHVJDRZVHVKDRZQZVJVKQZSZSZGITFITZUBZNUTZOZHIOZVLVMHCPZQZ
      VPWJQZUEZHVJDRZHVKDRZQZWBSZSZGITFITZUBZVFVGWGHVDVGWGQVEBCDENFGIJKLMUFUGUH
      VEWHWTUIVDWFWTNHAVHHQZVIWIWEWSVHHIUJXAWDWRFGIIXAVRWMWCWQXAVOWKVQWLVLXAVNW
      JVMVHHCUKZULXAVNWJVPXBULUMXAWAWPWBXAVSWNVTWOVHHVJDUNVHHVKDUNUOUPUQURUSVAV
      BVC $.
      $( [2-Jan-2008] $)

    $( The predicate "is a monomorphism".  (Contributed by FL, 2-Jan-2008.) $)
    ismonb1 $p |- ( ( T e. Cat /\ F e. M ) -> ( F e. ( Mono ` T ) <->
      A. g e. M A. h e. M
        ( ( ( D ` g ) = ( D ` h ) /\
            ( C ` g ) = ( D ` F ) /\
            ( C ` h ) = ( D ` F ) ) ->
              ( ( F R g ) = ( F R h ) -> g = h ) ) ) ) $=
      ( wcel wa cfv cv wceq co wi wral ccat cmon w3a ismonb ibar adantl bitr4d
      wb ) DUAMZGHMZNGDUBOMUJEPZBOFPZBOQUKAOGBOZQULAOUMQUCGUKCRGULCRQUKULQSSFHT
      EHTZNZUNHABCDEFGHIJKLUDUJUNUOUHUIUJUNUEUFUG $.
      $( [2-Jan-2008] $)
  $}

  ${
    $d C g j $.  $d D g j $.  $d F g j $.  $d G g j $.  $d J j $.  $d M g j $.
    $d R g j $.  $d T g j $.
    ismonb2.1 $e |- M = dom ( dom_ ` T ) $.
    ismonb2.2 $e |- D = ( dom_ ` T ) $.
    ismonb2.3 $e |- C = ( cod_ ` T ) $.
    ismonb2.4 $e |- R = ( o_ ` T ) $.
    $( A monomorphism is a left-cancelable morphism.  (Contributed by FL,
       2-Jan-2008.) $)
    ismonb2 $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ J e. M ) ) ->
      ( F e. ( Mono ` T ) ->
        ( ( ( D ` G ) = ( D ` J ) /\
            ( C ` G ) = ( D ` F ) /\
            ( C ` J ) = ( D ` F ) ) ->
              ( ( F R G ) = ( F R J ) -> G = J ) ) ) ) $=
      ( vg vj wcel w3a cfv wceq co wi ccat wa cmon cv wral wb ismonb1 3ad2antr1
      3simpc adantl fveq2 eqeq1d 3anbi12d oveq2 imbi12d eqeq2d 3anbi13d rcla42v
      eqeq1 eqeq2 syl sylbid ) DUAOZEHOZFHOZGHOZPZUBZEDUCQOZMUDZBQZNUDZBQZRZVJA
      QZEBQZRZVLAQZVPRZPZEVJCSZEVLCSZRZVJVLRZTZTZNHUEMHUEZFBQZGBQZRZFAQZVPRZGAQ
      ZVPRZPZEFCSZEGCSZRZFGRZTZTZVCVEVDVIWGUFVFABCDMNEHIJKLUGUHVHVEVFUBZWGXATVG
      XBVCVDVEVFUIUJWFXAWHVMRZWLVSPZWPWBRZFVLRZTZTMNFGHHVJFRZVTXDWEXGXHVNXCVQWL
      VSXHVKWHVMVJFBUKULXHVOWKVPVJFAUKULUMXHWCXEWDXFXHWAWPWBVJFECUNULVJFVLUSUOU
      OVLGRZXDWOXGWTXIXCWJVSWNWLXIVMWIWHVLGBUKUPXIVRWMVPVLGAUKULUQXIXEWRXFWSXIW
      BWQWPVLGECUNUPVLGFUTUOUOURVAVB $.
      $( [2-Jan-2008] $)
  $}

  ${
    $d B a g h $.  $d C a g h $.  $d F a g h $.  $d H a g h $.  $d O a g h $.
    $d R a $.  $d T a g h $.
    imonclem.1 $e |- O = dom ( id_ ` T ) $.
    imonclem.2 $e |- H = ( hom ` T ) $.
    imonclem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ ismonc . $)
    imonclem $p |- ( ( T e. Cat
                         /\ ( B e. O /\ C e. O )
                         /\ F e. ( H ` <. B , C >. ) )
                      -> ( A. a
                            e. O
                               A. g
                               e. ( H ` <. a , B >. )
                                  A. h
                                  e. ( H ` <. a , B >. )
                                     ( ( F R g ) = ( F R h ) -> g = h )
                         -> ( F e. dom ( dom_ ` T )
                            /\ A. g
                               e. dom ( dom_ ` T )
                                  A. h
                                  e. dom ( dom_ ` T )
                                     ( ( ( ( dom_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` h )
                                        /\ ( ( cod_ ` T ) ` g )
                                           = ( ( dom_ ` T ) ` F )
                                        /\ ( ( cod_ ` T ) ` h )
                                           = ( ( dom_ ` T ) ` F ) )
                                     -> ( ( F R g ) = ( F R h ) -> g = h ) ) )
                         ) ) $=
      ( wcel wa cfv wceq wi wral adantr ccat cop w3a cv co cdom_ cdm ccod_ eqid
      ehm 3expib 3imp wal ax-17 hbra2 hban hbral simp1 expcom mpan9 r2al ralbii
      dmo ax-4 a4s ralimi wb fveq2d eleq2d anbi12d imbi1d adantl simpll1 simp2l
      opeq1 ad2antrr ishomd syl3anc simplr simp2 eqcomd ad2antll eqtr3 3ad2ant3
      dehm ex com12 imp 3jca simplrl eqidd 3ad2ant2 simplrr jca impbida syl2anc
      eqtr 3bitrd rcla4dv syl5com sylbi mpdan impancom alrimi sylibr ) DUANZAIN
      ZBINZOZGABUBHPNZUCZGEUDZCUEGFUDZCUEQXLXMQRZFJUDZAUBZHPZSZEXQSZJISZGDUFPZU
      GZNZXLYAPZXMYAPZQZXLDUHPZPZGYAPZQZXMYGPZYIQZUCZXNRZFYBSEYBSZOXKXTOZYCYOXK
      YCXTXFXIXJYCXFXGXHXJYCRABDGHYBIKYBUIZLUJUKULTYPXLYBNZXMYBNZOZYNRZFUMZEUMY
      OYPUUBEXKXTEXKEUNXRJEIXQUOUPYPUUAFXKXTFXKFUNXSFJIXOINFUNXNEFXQXQUOUQUPXKY
      TXTYNXKYTOZYDINZXTYNRXKXFYTUUDXFXIXJURYRXFUUDRYSXFYRUUDYADXLYBIYQKYAUIZVC
      USTUTZXTUUCUUDOZYNXTXLXQNZXMXQNZOZXNRZFUMZEUMZJISZUUGYNRXSUUMJIXNEFXQXQVA
      VBUUNUUKJISUUGYNUUMUUKJIUULUUKEUUKFVDVEVFUUCUUKYNJYDIUUCXOYDQZOZUUKXLYDAU
      BZHPZNZXMUURNZOZXNRZYRYDYDQZYHAQZUCZYSYEYDQZYKAQZUCZOZXNRZYNUUOUUKUVBVGUU
      CUUOUUJUVAXNUUOUUHUUSUUIUUTUUOXQUURXLUUOXPUUQHXOYDAVOVHZVIUUOXQUURXMUVKVI
      VJVKVLUUPXFUUDXGUVBUVJVGXFXIXJYTUUOVMUUCUUDUUOUUFTXKXGYTUUOXFXGXHXJVNVPXF
      UUDXGUCZUVAUVIXNUVLUUSUVEUUTUVHYDAYGYADXLHYBIKYQUUEYGUIZLVQYDAYGYADXMHYBI
      KYQUUEUVMLVQVJVKVRUUPUVIYMXNUUPYIAQZYTUVIYMVGXKUVNYTUUOXFXIXJUVNXFXGXHXJU
      VNRABYADGHIKUUELWEUKULVPXKYTUUOVSUVNYTOZUVIYMUVOUVIOYFYJYLUVHYFUVOUVEUVHY
      EYDYSUVFUVGVTWAWBUVOUVIYJUVNUVIYJRYTUVIUVNYJUVEUVNYJRZUVHUVDYRUVPUVCUVDUV
      NYJYHYIAWCWFWDTWGTWHUVOUVIYLUVNUVIYLRYTUVIUVNYLUVHUVNYLRZUVEUVGYSUVQUVFUV
      GUVNYLYKYIAWCWFWDVLWGTWHWIUVOYMOZUVEUVHUVRYRUVCUVDUVNYRYSYMWJUVRYDWKUVOYM
      UVDUVNYMUVDRYTYMUVNUVDYJYFUVNUVDRYLYJUVNUVDYHYIAWQWFWLWGTWHWIUVRYSUVFUVGU
      VNYRYSYMWMYMUVFUVOYMYDYEYFYJYLURWAVLUVOYMUVGUVNYMUVGRYTYMUVNUVGYLYFUVNUVG
      RYJYLUVNUVGYKYIAWQWFWDWGTWHWIWNWOWPVKWRWSWTXAWGXBXCXDXDYNEFYBYBVAXEWNWF
      $.
      $( [1-Jan-2008] $)
  $}

  ${
    $d B a g h $.  $d C a g h $.  $d F a g h $.  $d H a g h $.  $d O a g h $.
    $d R a $.  $d T a g h $.
    ismonc.1 $e |- O = dom ( id_ ` T ) $.
    ismonc.2 $e |- H = ( hom ` T ) $.
    ismonc.3 $e |- R = ( o_ ` T ) $.
    $( The predicate "is a monomorphism" when the morphism belongs to a
       homset.  (Contributed by FL, 2-Jan-2008.) $)
    ismonc $p |- ( ( T e. Cat /\ ( B e. O /\ C e. O ) /\
      F e. ( H ` <. B , C >. ) ) -> ( F e. ( Mono ` T ) <->
        A. a e. O A. g e. ( H ` <. a , B >. ) A. h e. ( H ` <. a , B >. )
        ( ( F R g ) = ( F R h ) -> g = h ) ) ) $=
      ( wcel wa cfv w3a wceq wi wral ccat cop cmon cdom_ cdm cv ccod_ co ismonb
      eqid 3adant2 wal ax-17 hbra1 hban hbra2 ra42 pm3.31 simpll1 simplr simp2l
      ad2antrr ishomd biimpd anim12d syl3anc simp1 anim12i a1i dehm simp2 eqtr3
      wb syl2an adantl 3ad2ant3 adantr impcom 3jca syl6 3expib 3imp jcad imim1d
      syld com13 3syl alrimi exp31 com23 imp alrimiv ralbii df-ral bitri sylibr
      ex r2al imonclem impbid bitrd ) DUANZAINZBINZOZGABUBHPZNZQZGDUCPNZGDUDPZU
      EZNZEUFZXJPZFUFZXJPZRZXMDUGPZPZGXJPZRZXOXRPZXTRZQZGXMCUHGXOCUHRXMXORSZSZF
      XKTZEXKTZOZYEFJUFZAUBHPZTEYKTZJITZXBXGXIYIVMXEXFXRXJCDEFGXKXKUJZXJUJZXRUJ
      ZMUIUKXHYIYMXHYIYMXHYIOZYJINZXMYKNZXOYKNZOZYESZFULZEULZSZJULZYMYQUUEJXHYI
      UUEXHYRYIUUDXHYRYIUUDXHYROZYIOZUUCEUUGYIEUUGEUMXLYHEXLEUMYGEXKUNUOUOUUHUU
      BFUUGYIFUUGFUMXLYHFXLFUMYFEFXKXKUPUOUOYIUUGUUBYHXLUUGUUBSZYHXMXKNZXOXKNZO
      ZYFSUULYDOZYESZXLUUISYFEFXKXKUQUULYDYEURUUGXLUUNUUBUUGXLUUNUUBSUUGXLOZUUA
      UUMYEUUOUUAUUJXNYJRZXSARZQZUUKXPYJRZYBARZQZOZUUMUUOXBYRXCUUAUVBSXBXEXGYRX
      LUSXHYRXLUTXHXCYRXLXBXCXDXGVAVBXBYRXCQZYSUURYTUVAUVCYSUURYJAXRXJDXMHXKIKY
      NYOYPLVCVDUVCYTUVAYJAXRXJDXOHXKIKYNYOYPLVCVDVEVFUUOUVBUULYDUVBUULSUUOUURU
      UJUVAUUKUUJUUPUUQVGUUKUUSUUTVGVHVIXHUVBYDSZYRXLXBXEXGUVDXBXCXDXGUVDSXBXCX
      DQXGXTARZUVDABXJDGHIKYOLVJUVEUVBYDUVEUVBOXQYAYCUVBXQUVEUURUUPUUSXQUVAUUJU
      UPUUQVKUUKUUSUUTVKXNXPYJVLVNVOUVBUVEYAUURUVEYASZUVAUUQUUJUVFUUPUUQUVEYAXS
      XTAVLWQVPVQVRUVBUVEYCUVAUVEYCSZUURUUTUUKUVGUUSUUTUVEYCYBXTAVLWQVPVOVRVSWQ
      VTWAWBVBWCWEWDWQWFWGVRVRWHWHWIWJWKWLYMUUDJITUUFYLUUDJIYEEFYKYKWRWMUUDJIWN
      WOWPWQABCDEFGHIJKLMWSWTXA $.
      $( [2-Jan-2008] $)
  $}


  ${
    $d A a g h $.  $d B a g h k l $.  $d C a g h k l $.  $d F a g h k l $.
    $d G a g h k l $.  $d H a g h k l $.  $d O a g h k l $.  $d R a g h k l $.
    $d T a g h k l $.
    cmpmon.1 $e |- O = dom ( id_ ` T ) $.
    cmpmon.2 $e |- H = ( hom ` T ) $.
    cmpmon.3 $e |- R = ( o_ ` T ) $.
    $( The composite of two monomorphisms is a monomorphism.  JFM CAT_1 th. 61
       (Contributed by FL, 10-Mar-2008.) $)
    cmpmon $p |- ( ( T e. Cat /\ ( ( A e. O /\ B e. O /\ C e. O ) /\
         ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) )
           /\ ( F e. ( Mono ` T ) /\ G e. ( Mono ` T ) ) ) ) ->
              ( G R F ) e. ( Mono ` T ) ) $=
      ( vg vh va wcel wa wi wceq ex vk vl ccat w3a cop cmon co cv 3simpa ismonc
      cfv wral biimpd 3exp com13 adantr syl5 3imp 3simpc ax-17 hbra1 hban hbra2
      adantl hbral cmpassoh 3expib com3r com23 com14 exp43 3impd com34 ad2antrr
      imp31 eqcom biimpi eqcomd 3exp1 com3l com12 impcom 3impia 3eqtr2d adantrd
      imp com4r homgrf com24 3adant3 jca ra4 ad2antll oveq2 eqeq1d eqeq1 eqeq2d
      imbi12d eqeq2 rcla42v sylc wal r3al 19.21bi 19.21bbi ad2antrl 3expd imp41
      3syld ralrimi wb simp1 3simpb 3ad2ant2 syl3anc sylibrd syl2and 3imp2 ) EU
      CPZAIPZBIPZCIPZUDZFABUEHUKPZGBCUEHUKPZQZFEUFUKZPZGYGPZQZGFDUGZYGPZXSYCYFY
      JYLRXSYCYFUDZYHFMUHZDUGZFNUHZDUGZSZYNYPSZRZNOUHZAUEHUKZULZMUUBULZOIULZYIG
      UAUHZDUGZGUBUHZDUGZSZUUFUUHSZRZUBUUABUEHUKZULUAUUMULZOIULZYLXSYCYFYHUUERZ
      YCXTYAQZXSYFUUPRXTYAYBUIZYFUUQXSUUPYDUUQXSUUPRRYEXSUUQYDUUPXSUUQYDUUPXSUU
      QYDUDYHUUEABDEMNFHIOJKLUJUMUNUOUPUOUQURXSYCYFYIUUORZYCYAYBQZXSYFUUSRXTYAY
      BUSYFUUTXSUUSYEUUTXSUUSRRYDXSUUTYEUUSXSUUTYEUUSXSUUTYEUDYIUUOBCDEUAUBGHIO
      JKLUJUMUNUOVDUOUQURYMUUEUUOQZYKYNDUGZYKYPDUGZSZYSRZNUUBULZMUUBULZOIULZYLY
      MUVAUVHYMUVAQZUVGOIYMUVAOYMOUTUUEUUOOUUDOIVAUUNOIVAVBVBUVIUUAIPZUVGUVIUVJ
      QZUVFMUUBUVIUVJMYMUVAMYMMUTUUEUUOMUUCOMIUUBVCUUOMUTVBVBUVJMUTVBUVKYNUUBPZ
      UVFUVKUVLQZUVENUUBUVKUVLNUVIUVJNYMUVANYMNUTUUEUUONUUDNOIUVJNUTZYTMNUUBUUB
      VCVEUUONUTVBVBUVNVBUVLNUTVBUVMYPUUBPZUVEUVMUVOQZUVDGYODUGZGYQDUGZSZYRYSUV
      PUVDUVSUVPUVDQUVQUVBUVCUVRUVMUVQUVBSZUVOUVDUVIUVJUVLUVTYMUVJUVLUVTRZRZUVA
      XSYCYFUWBXSYCUVJYFUWAUVJYCXSYFUWARZUVJXTYAYBXSUWCRZUVJXTYAYBUWDUVLXSYFUVJ
      XTQZUUTQZUVTUVLYFXSUWFUVTRZUVLYDYEXSUWGRXSUWFUVLYDYEUDZUVTXSUWEUUTUWHUVTR
      UUAABCDEHYNFGIJKLVFVGVHVGVIVJVKVLUOVMURUPVOVNUVDUVCUVBSZUVPUVDUWIUVBUVCVP
      VQVDUVPUVCUVRSZUVDUVMUVOUWJUVKUVOUWJRZUVLUVIUVJUWKYMUVJUWKRZUVAXSYCYFUWLU
      VOYFUVJXSYCQZUWJUVOYDYEUVJUWMUWJRRUWMUVJUVOYDYEUDZUWJYCXSUVJUWNUWJRZRZXTY
      AYBXSUWPRYAYBXSXTUWPYAYBXSXTUWPRZRXSUUTUWQUVJUUTXTXSUWOUVJXTUUTXSUWORZUVJ
      XTUUTUWRRXSUWEUUTUWOXSUWEUUTUWNUWJXSUWEUUTUDZUWNQUVRUVCUWSUWNUVRUVCSUUAAB
      CDEHYPFGIJKLVFWFVRVSVTTVIVJWATWGURWBUOVGVJWCUPWFUPWFUPWDTUVPYOUUMPZYQUUMP
      ZQUUNUVSYRRZUVPUWTUXAUVMUWTUVOUVIUVJUVLUWTYMUVJUVLUWTRZRZUVAXSYCYFUXDYFYC
      XSUXDYDYCXSUXDRZRYEYCUUQYDUXEUURUVJUUQXSYDUXCUVJXTYAXSYDUXCRRUVLXSYDUVJXT
      YAUDZUWTUVLYDXSUXFUWTRZUVLYDXSUXGRXSUXFUVLYDQZUWTXSUXFUXHUWTRUUAABDEYNFHI
      JKLWHTVHTVIVJVGVJUQUPUOURUPVOUPUVMUVOUXAUVKUVOUXARZUVLUVIUVJUXIYMUVJUXIRZ
      UVAXSYCYFUXJYCXSYFUXJRZXTYAXSUXKRYBUUQUVJYFXSUXIUVJUUQYFXSUXIRZRZUVJXTYAU
      XMUXFYDUXLYEUVOYDXSUXFUXAUVOYDXSUXFUXARRXSUXFUVOYDQZUXAXSUXFUXNUXARUUAABD
      EYPFHIJKLWHTVHTVJWEVGWAWIWJWAURUPWFUPWFWKUVKUUNUVLUVOUVIUVJUUNUUOUVJUUNRY
      MUUEUUNOIWLWMWFVNUULUXBUVQUUISZYOUUHSZRUAUBYOYQUUMUUMUUFYOSZUUJUXOUUKUXPU
      XQUUGUVQUUIUUFYOGDWNWOUUFYOUUHWPWRUUHYQSZUXOUVSUXPYRUXRUUIUVRUVQUUHYQGDWN
      WQUUHYQYOWSWRWTXAUVIUVJUVLUVOYTUVIUVJUVLUVOYTUUEUVJUVLUVOUDYTRZYMUUOUUEUX
      SMNUUEUXSNXBMXBZOUUEUXTOXBYTOMNIUUBUUBXCVQXDXEXFXGXHXITXJTXJTXJTYMXSXTYBQ
      ZYKACUEHUKPZYLUVHXKXSYCYFXLYCXSUYAYFXTYAYBXMXNXSYCYFUYBABCDEFGHIJKLWHWCAC
      DEMNYKHIOJKLUJXOXPXQUNXR $.
      $( [10-Mar-2008] $)
  $}

  ${
    $d A a m n $.  $d B a m n $.  $d C a m n $.  $d F a m n $.  $d G a m n $.
    $d H a m n $.  $d O a m n $.  $d R a m n $.  $d T a m n $.
    icmpmon.1 $e |- O = dom ( id_ ` T ) $.
    icmpmon.2 $e |- H = ( hom ` T ) $.
    icmpmon.3 $e |- R = ( o_ ` T ) $.
    $( If ` ( G R F ) ` is a monomorphism then ` F ` is a monomorphism.  JFM
       CAT_1 th. 62 (Contributed by FL, 17-Mar-2008.) $)
    icmpmon $p |- ( ( T e. Cat /\ ( ( A e. O /\ B e. O /\ C e. O ) /\
         ( F e. ( H ` <. A , B >. ) /\ G e. ( H ` <. B , C >. ) )
           /\ ( G R F ) e. ( Mono ` T ) ) ) ->
              F e. ( Mono ` T ) ) $=
      ( vm vn va wcel wa co wceq wi ccat w3a cop cmon cv wral wb simp1 3ad2ant2
      3simpb homgrf 3impia ismonc syl3anc cmpassoh 3exp com3l exp4b 3impd com13
      cfv 3expib com14 imp31 eqcomd adantr 3exp1 com23 3imp oveq2 simpll simplr
      simpr 3eqtr4d ex imim1d syl7 syl2anc ralimdva sylbid 3imp2 simpl 3ad2ant1
      imp 3simpa adantl simpr2l mpbird ) EUAPZAIPZBIPZCIPZUBZFABUCHVAPZGBCUCHVA
      PZQZGFDRZEUDVAZPZUBZQZFWRPZFMUEZDRZFNUEZDRZSZXCXESZTZNOUEZAUCHVAZUFZMXKUF
      ZOIUFZWIWMWPWSXNWIWMWPWSXNTWIWMWPUBZWSWQXCDRZWQXEDRZSZXHTZNXKUFZMXKUFZOIU
      FZXNXOWIWJWLQZWQACUCHVAPZWSYBUGWIWMWPUHWMWIYCWPWJWKWLUJUIWIWMWPYDABCDEFGH
      IJKLUKULACDEMNWQHIOJKLUMUNXOYAXMOIXOXJIPZQZXTXLMXKYFXCXKPZQZXSXINXKYHXEXK
      PZQXPGXDDRZSZXQGXFDRZSZXSXITYHYKYIYHYJXPXOYEYGYJXPSZWIWMWPYEYGYNTTYGWPYEW
      IWMQZYNYGWNWOYEYOYNTTYOYEYGWNWOUBZYNWIWMYEYPYNTZTYEWMWIYQYEWJWKWLWIYQTZYE
      WJWKWLYRWIYEWJQZWKWLQZYQWIYSYTYQXJABCDEHXCFGIJKLUOUPUQURUSUTWDUTVBVCULVDV
      EVFYHYIYMYFYIYMTZYGXOYEUUAXOYIYEYMWIWMWPYIYEYMTZTYIWMWPWIUUBYIWPWMWIUUBTZ
      YIWNWOWMUUCTYEWMWIYIWNWOUBZYMYEWJWKWLWIUUDYMTZTZYEWJWKWLUUFWIYSYTUUEWIYSY
      TUUDYMWIYSYTUBZUUDQYLXQUUGUUDYLXQSXJABCDEHXEFGIJKLUOWDVEVGUQURUSVCVBVHVCV
      IVHWDVFWDXGYJYLSZYKYMQZXSXHXDXFGDVJUUIUUHXRXHUUIUUHXRUUIUUHQYJYLXPXQUUIUU
      HVMYKYMUUHVKYKYMUUHVLVNVOVPVQVRVSVSVSVTUPWAXAWIWJWKQZWNXBXNUGWIWTWBWTUUJW
      IWMWPUUJWSWJWKWLWEWCWFWNWOWMWSWIWGABDEMNFHIOJKLUMUNWH $.
      $( [27-Apr-2008] $) $( [17-Mar-2008] $)
  $}

  ${
    $d A c f g $.  $d B c f g $.  $d F c f g $.  $d G c f g $.  $d H c f g $.
    $d J c f g $.  $d O c f g $.  $d R c f g $.  $d T c f g $.
    idmon.1 $e |- O = dom ( id_ ` T ) $.
    idmon.2 $e |- H = ( hom ` T ) $.
    idmon.3 $e |- R = ( o_ ` T ) $.
    idmon.4 $e |- J = ( id_ ` T ) $.
    $( If there exists ` G ` such as ` ( G R F ) = ( J `` B ) ` then F is a
       monomorphism.  JFM CAT_1 th. 63.  (Contributed by FL, 5-May-2008.) $)
    idmon $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O ) /\
      ( G e. ( H ` <. A , B >. ) /\ F e. ( H ` <. B , A >. ) ) ) ->
      ( ( G R F ) = ( J ` B ) -> F e. ( Mono ` T ) ) ) $=
      ( wcel wa cfv w3a co wceq ad2antrr vf vg vc ccat cop cmon cv oveq2 simpl1
      wi wral simplrl simpl2r jca simpl2l 3jca simplrr simpl3r simpl3l cmpassoh
      sylc simpr eqeq12d simpllr oveq1d biimpd simpll1 cmphmia syl3anc impr cdm
      adantr cdom_ ccod_ cid_ eqcomi dmeqi eleq2i biimpi adantl 3ad2ant2 simprl
      eqtri eqid ehm imp cehm cmpida sylibd sylbid syl5 ralrimiva ralrimivva wb
      ismonc syl121anc mpbird ex ) DUDNZAINZBINZOZFABUEGPNZEBAUEGPNZOZQZFECRZBH
      PZSZEDUFPNZXFXIOZXJEUAUGZCRZEUBUGZCRZSZXLXNSZUJZUBUCUGZBUEGPZUKZUAXTUKUCI
      UKZXKYAUCUAIXTXKXSINZXLXTNZOZOZXRUBXTXPFXMCRZFXOCRZSZYFXNXTNZOZXQXMXOFCUH
      YKYIXGXLCRZXGXNCRZSZXQYKYGYLYHYMYKWSYCXAOZXBQZYDXDXCQYGYLSYKWSYOXBXKWSYEY
      JWSXBXEXIUIZTZYKYCXAXKYCYDYJULXKXAYEYJWTXAWSXEXIUMZTZUNYKWTXAXKWTYEYJWTXA
      WSXEXIUOZTYTUNUPZYKYDXDXCXKYCYDYJUQXKXDYEYJXCXDWSXBXIURZTZXKXCYEYJXCXDWSX
      BXIUSTZUPXSBABCDGXLEFIJKLUTVAYKYPYJXDXCQYHYMSUUBYKYJXDXCYFYJVBUUDUUEUPXSB
      ABCDGXNEFIJKLUTVAVCYKYNXHXLCRZXHXNCRZSZXQYKYNUUHYKYLUUFYMUUGYKXGXHXLCXFXI
      YEYJVDZVEYKXGXHXNCUUIVEVCVFYKUUFXLUUGXNYFUUFXLSZYJXKYCYDUUJXKYCOWSYCXAYDU
      UJUJWSXBXEXIYCVGXKYCVBXKXAYCYSVLXSBCDXLGHIJKMLVHVIVJVLYKWSBHVKZNZXNDVMPZV
      KZNZQXNDVNPZPBSZUUGXNSYKWSUULUUOYRXKUULYEYJXFUULXIXBWSUULXEXAUULWTXAUULIU
      UKBIDVOPZVKUUKJUURHHUURMVPVQWCVRVSVTWAVLTYFYJUUOYFWSYCXAYJUUOUJWSXBXEXIYE
      VGZXKYCYDWBZXKXAYEYSVLZXSBDXNGUUNIJUUNWDZKWEVIWFUPYFYJUUQYFWSYCXAYJUUQUJU
      USUUTUVAXSBUUPDXNGIJUUPWDZKWGVIWFBUUPUUMCDXNHUUNUUKUVBUUMWDUUKWDMLUVCWHVA
      VCWIWJWKWLWMXKWSXAWTXDXJYBWNYQYSUUAUUCBACDUAUBEGIUCJKLWOWPWQWR $.
      $( [1-Jun-2008] $) $( [5-May-2008] $)
  $}

  ${
    immon.1 $e |- O = dom ( id_ ` T ) $.
    immon.2 $e |- J = ( id_ ` T ) $.
    $( A morphism identity is a monomorphism.  JFM CAT_1 th. 64.  (Contributed
       by FL, 5-May-2008.) $)
    immon $p |- ( ( T e. Cat /\ A e. O ) -> ( J ` A ) e. ( Mono ` T ) ) $=
      ( ccat wcel wa cfv cop chom w3a co_ co wceq cmon simpl jca eqid simpr iri
      homib 3jca idmon sylc ) BGHZADHZIZUGUHUHIZACJZAAKBLJZJHZUMIZMUKUKBNJZOUKP
      UKBQJHUIUGUJUNUGUHRUIUHUHUGUHUAZUPSUIUMUMABULCDEFULTZUCZURSUDAUOBCDEFUOTZ
      UBAAUOBUKUKULCDEUQUSFUEUF $.
      $( [1-Jun-2008] $) $( [5-May-2008] $)
  $}

  ${
    $d C x y $.  $d D x y $.  $d M f g h x y $.  $d R x y $.  $d T f g h x y $.
    isepia.1 $e |- M = dom ( dom_ ` T ) $.
    isepia.2 $e |- D = ( dom_ ` T ) $.
    isepia.3 $e |- C = ( cod_ ` T ) $.
    isepia.4 $e |- R = ( o_ ` T ) $.
    $( Epimorphisms of a category ` T ` .  (Contributed by FL, 8-Aug-2008.) $)
    isepia $p |- ( T e. Cat -> ( Epic ` T ) = { f | ( f e. M /\
      A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` f ) /\
            ( D ` h ) = ( C ` f ) ) ->
              ( ( g R f ) = ( h R f ) -> g = h ) ) ) } ) $=
      ( cv cdom_ cfv wceq co wi wral fveq1d vx vy cdm wcel ccod_ w3a co_ wa cab
      ccat cepi fveq2 dmeqd syl6eqr eleq2d eqcomi eqtrd eqeq12d 3anbi123d oveqd
      imbi1d imbi12d 2ralbidv raleqdv ralbidv 3bitrd anbi12d abbidv df-epi fvex
      a1i cvv dmex eqeltri zfausab fvopab4 ) UAUBDEMZUAMZNOZUCZUDZFMZVRUEOZOZGM
      ZWCOZPZWBVSOZVQWCOZPZWEVSOZWIPZUFZWBVQVRUGOZQZWEVQWNQZPZWBWEPZRZRZGVTSFVT
      SZUHZEUIVQHUDZWBAOZWEAOZPZWBBOZVQAOZPZWEBOZXHPZUFZWBVQCQZWEVQCQZPZWRRZRZG
      HSZFHSZUHZEUIUJUKVRDPZXBXTEYAWAXCXAXSYAVTHVQYAVTDNOZUCZHYAVSYBVRDNULZUMIU
      NZUOYAXAXQGVTSZFVTSXRFVTSXSYAWTXQFGVTVTYAWMXLWSXPYAWGXFWJXIWLXKYAWDXDWFXE
      YAWDWBDUEOZOXDYAWBWCYGVRDUEULZTYAWBYGAYGAPYAAYGKUPVKZTUQYAWEWCAYAWCYGAYHK
      UNTURYAWHXGWIXHYAWBVSBYAVSYBBYDJUNZTYAWIVQYGOXHYAVQWCYGYHTYAVQYGAYITUQZUR
      YAWKXJWIXHYAWEVSBYJTYKURUSYAWQXOWRYAWOXMWPXNYAWOWBVQDUGOZQXMYAWNYLWBVQVRD
      UGULZUTYAYLCWBVQYLCPYACYLLUPVKZUTUQYAWPWEVQYLQXNYAWNYLWEVQYMUTYAYLCWEVQYN
      UTUQURVAVBVCYAYFXRFVTYAXQGVTHYEVDVEYAXRFVTHYEVDVFVGVHUAUBEFGVIXSEHHYCVLIY
      BDNVJVMVNVOVP $.
      $( [1-Sep-2008] $) $( [8-Aug-2008] $)
  $}

  ${
    $d C f $.  $d D f $.  $d F f g h $.  $d M f g h $.  $d R f $.
    $d T f g h $.
    isepib.1 $e |- M = dom ( dom_ ` T ) $.
    isepib.2 $e |- D = ( dom_ ` T ) $.
    isepib.3 $e |- C = ( cod_ ` T ) $.
    isepib.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism".  (Contributed by FL, 8-Aug-2008.) $)
    isepib $p |- ( ( T e. Cat /\ F e. A ) -> ( F e. ( Epic ` T )
      <-> ( F e. M /\ A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` F ) /\
            ( D ` h ) = ( C ` F ) ) ->
              ( ( g R F ) = ( h R F ) -> g = h ) ) ) ) ) $=
      ( vf wcel cfv wceq co wi wral ccat wa cepi cv w3a isepia adantr eleq2d wb
      eleq1 fveq2 eqeq2d 3anbi23d oveq2 eqeq12d imbi1d imbi12d 2ralbidv anbi12d
      cab elabg adantl bitrd ) EUAOZHAOZUBZHEUCPZOHNUDZIOZFUDZBPGUDZBPQZVJCPZVH
      BPZQZVKCPZVNQZUEZVJVHDRZVKVHDRZQZVJVKQZSZSZGITFITZUBZNUTZOZHIOZVLVMHBPZQZ
      VPWJQZUEZVJHDRZVKHDRZQZWBSZSZGITFITZUBZVFVGWGHVDVGWGQVEBCDENFGIJKLMUFUGUH
      VEWHWTUIVDWFWTNHAVHHQZVIWIWEWSVHHIUJXAWDWRFGIIXAVRWMWCWQXAVOWKVQWLVLXAVNW
      JVMVHHBUKZULXAVNWJVPXBULUMXAWAWPWBXAVSWNVTWOVHHVJDUNVHHVKDUNUOUPUQURUSVAV
      BVC $.
      $( [1-Sep-2008] $) $( [8-Aug-2008] $)
  $}

  ${
    $d F g h $.  $d M g h $.  $d T g h $.
    isepib1.1 $e |- M = dom ( dom_ ` T ) $.
    isepib1.2 $e |- D = ( dom_ ` T ) $.
    isepib1.3 $e |- C = ( cod_ ` T ) $.
    isepib1.4 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism".  (Contributed by FL, 10-Aug-2008.) $)
    isepib1 $p |- ( ( T e. Cat /\ F e. M ) -> ( F e. ( Epic ` T ) <->
      A. g e. M A. h e. M
        ( ( ( C ` g ) = ( C ` h ) /\
            ( D ` g ) = ( C ` F ) /\
            ( D ` h ) = ( C ` F ) ) ->
              ( ( g R F ) = ( h R F ) -> g = h ) ) ) ) $=
      ( wcel wa cfv cv wceq co wi wral ccat cepi w3a isepib ibar adantl bitr4d
      wb ) DUAMZGHMZNGDUBOMUJEPZAOFPZAOQUKBOGAOZQULBOUMQUCUKGCRULGCRQUKULQSSFHT
      EHTZNZUNHABCDEFGHIJKLUDUJUNUOUHUIUJUNUEUFUG $.
      $( [1-Sep-2008] $) $( [10-Aug-2008] $)
  $}

  ${
    $d C g j $.  $d D g j $.  $d F g j $.  $d G g j $.  $d J j $.  $d M g j $.
    $d R g j $.  $d T g j $.
    isepib2.1 $e |- M = dom ( dom_ ` T ) $.
    isepib2.2 $e |- D = ( dom_ ` T ) $.
    isepib2.3 $e |- C = ( cod_ ` T ) $.
    isepib2.4 $e |- R = ( o_ ` T ) $.
    $( An epimorphism is a right-cancelable morphism.  (Contributed by FL,
       10-Aug-2008.) $)
    isepib2 $p |- ( ( T e. Cat /\ ( F e. M /\ G e. M /\ J e. M ) ) ->
      ( F e. ( Epic ` T ) ->
        ( ( ( C ` G ) = ( C ` J ) /\
            ( D ` G ) = ( C ` F ) /\
            ( D ` J ) = ( C ` F ) ) ->
              ( ( G R F ) = ( J R F ) -> G = J ) ) ) ) $=
      ( vg vj wcel w3a cfv wceq co wi ccat wa cepi cv wral wb isepib1 3ad2antr1
      3simpc adantl fveq2 eqeq1d 3anbi12d oveq1 imbi12d eqeq2d 3anbi13d rcla42v
      eqeq1 eqeq2 syl sylbid ) DUAOZEHOZFHOZGHOZPZUBZEDUCQOZMUDZAQZNUDZAQZRZVJB
      QZEAQZRZVLBQZVPRZPZVJECSZVLECSZRZVJVLRZTZTZNHUEMHUEZFAQZGAQZRZFBQZVPRZGBQ
      ZVPRZPZFECSZGECSZRZFGRZTZTZVCVEVDVIWGUFVFABCDMNEHIJKLUGUHVHVEVFUBZWGXATVG
      XBVCVDVEVFUIUJWFXAWHVMRZWLVSPZWPWBRZFVLRZTZTMNFGHHVJFRZVTXDWEXGXHVNXCVQWL
      VSXHVKWHVMVJFAUKULXHVOWKVPVJFBUKULUMXHWCXEWDXFXHWAWPWBVJFECUNULVJFVLUSUOU
      OVLGRZXDWOXGWTXIXCWJVSWNWLXIVMWIWHVLGAUKUPXIVRWMVPVLGBUKULUQXIXEWRXFWSXIW
      BWQWPVLGECUNUPVLGFUTUOUOURVAVB $.
      $( [1-Sep-2008] $) $( [10-Aug-2008] $)
  $}

  ${
    $d A c g h $.  $d B c g h $.  $d F c g h $.  $d H c g h $.  $d O c g h $.
    $d R c $.  $d T c g h $.
    iepiclem.1 $e |- O = dom ( id_ ` T ) $.
    iepiclem.2 $e |- H = ( hom ` T ) $.
    iepiclem.3 $e |- R = ( o_ ` T ) $.
    $( Lemma for ~ isepic . $)
    iepiclem $p |- ( ( T e. Cat
                         /\ ( A e. O /\ B e. O )
                         /\ F e. ( H ` <. A , B >. ) )
                      -> ( A. c
                            e. O
                               A. g
                               e. ( H ` <. B , c >. )
                                  A. h
                                  e. ( H ` <. B , c >. )
                                     ( ( g R F ) = ( h R F ) -> g = h )
                         -> ( F e. dom ( dom_ ` T )
                            /\ A. g
                               e. dom ( dom_ ` T )
                                  A. h
                                  e. dom ( dom_ ` T )
                                     ( ( ( ( cod_ ` T ) ` g )
                                           = ( ( cod_ ` T ) ` h )
                                        /\ ( ( dom_ ` T ) ` g )
                                           = ( ( cod_ ` T ) ` F )
                                        /\ ( ( dom_ ` T ) ` h )
                                           = ( ( cod_ ` T ) ` F ) )
                                     -> ( ( g R F ) = ( h R F ) -> g = h ) ) )
                         ) ) $=
      ( wcel wa cfv w3a wceq wi wb ccat cop cv co wral cdom_ cdm ccod_ eqid ehm
      3expib 3imp adantr wal ax-17 hbra1 hban cdmo ex adantrd 3ad2ant1 imp ra42
      hbral ralimi opeq2 fveq2d eleq2d anbi12d anim12d ancrd ishomd 3exp adantl
      com12 impcom 3adant3 adantrr biimpd eqcoms 3anbi2d biimpi 3ad2ant3 simpl2
      eqeq2 eqcom simpr2 3jca simpll eqidd simplr simpr3 impbid2 sylan9bb bitrd
      jca syld sylan9bbr imbi1d rcla4dv syl5 mpdan impancom alrimi r2al sylibr
      syl ) DUANZAINZBINZOZGABUBHPNZQZEUCZGCUDFUCZGCUDRXNXORSZFBJUCZUBZHPZUEZEX
      SUEZJIUEZGDUFPZUGZNZXNDUHPZPZXOYFPZRZXNYCPZGYFPZRZXOYCPZYKRZQZXPSZFYDUEEY
      DUEZOXMYBOZYEYQXMYEYBXHXKXLYEXHXIXJXLYESABDGHYDIKYDUIZLUJUKULUMYRXNYDNZXO
      YDNZOZYPSZFUNZEUNYQYRUUDEXMYBEXMEUOYAEJIXQINZEUOXTEXSUPVDUQYRUUCFXMYBFXMF
      UOYAFJIUUEFUOXTFEXSXNXSNZFUOXPFXSUPVDVDUQXMUUBYBYPXMUUBOZYGINZYBYPSXMUUBU
      UHXHXKUUBUUHSXLXHYTUUHUUAXHYTUUHYFDXNYDIYSKYFUIZURUSZUTVAVBYBUUFXOXSNZOZX
      PSZJIUEUUGUUHOYPYAUUMJIXPEFXSXSVCVEUUGUUMYPJYGIUUGXQYGRZOUULYOXPUUNUULXNB
      YGUBZHPZNZXOUUPNZOZUUGYOUUNUUFUUQUUKUURUUNXSUUPXNUUNXRUUOHXQYGBVFVGZVHUUN
      XSUUPXOUUTVHVIXMUUBUUSYOTZXMUUBUUHYHINZOZUUBOZUVAXMUUBUVCXHXKUUBUVCSXLXHY
      TUUHUUAUVBUUJXHUUAUVBYFDXOYDIYSKUUIURUSVJVAVKXMUVDUVAXMUVDOUUSYTYJBRZYGYG
      RZQZUUAYMBRZYHYGRZQZOZYOXMUVCUUSUVKTUUBXMUVCOUUQUVGUURUVJUVCXMUUQUVGTZUUH
      XMUVLSUVBXMUUHUVLXHXKUUHUVLSZXLXKXHUVMXJXHUVMSXIXHXJUVMXHXJUUHUVLBYGYFYCD
      XNHYDIKYSYCUIZUUILVLVMVOVNVPVQVOUMVPUVCXMUURUVJTZUUHXMUVOSUVBXMUUHUVOXHXK
      UUHUVOSZXLXKXHUVPXJXHUVPSXIXHXJUVPXHXJUUHUVOBYGYFYCDXOHYDIKYSUVNUUILVLVMV
      OVNVPVQVOUMVPVIVRUVDXMUVKYOTZUUBXMUVQSUVCXMUUBUVQXMYEGYCPARZYKBRZQZUUBUVQ
      SZXHXKXLUVTXHXIXJXLUVTSXHXIXJQXLUVTABYFYCDGHYDIKYSUVNUUILVLVSUKULUVSYEUWA
      UVRUVSUUBUVQUVSUVKYTYLUVFQZUUAYNUVIQZOZUUBYOUVSUVGUWBUVJUWCUVSUVEYLYTUVFU
      VEYLTBYKBYKYJWEVTWAUVSUVHYNUUAUVIUVHYNTBYKBYKYMWEVTWAVIUUBUWDYOUWDYIYLYNU
      WCYIUWBUVIUUAYIYNUVIYIYHYGWFWBWCVNYTYLUVFUWCWDUWBUUAYNUVIWGWHUUBYOUWDUUBY
      OOZUWBUWCUWEYTYLUVFYTUUAYOWIUUBYIYLYNWGUWEYGWJWHUWEUUAYNUVIYTUUAYOWKUUBYI
      YLYNWLYOUVIUUBYIYLUVIYNYIUVIYGYHWFWBVAVNWHWPUSWMWNUSWCXGVOVNVPWOUSWQVBWRW
      SWTXAXBXCXDXDYPEFYDYDXEXFWPUS $.
      $( [7-Oct-2008] $) $( [6-Oct-2008] $)
  $}

  ${
    $d A c g h $.  $d B c g h $.  $d F c g h $.  $d H c g h $.  $d O c g h $.
    $d R c $.  $d T c g h $.
    isepic.1 $e |- O = dom ( id_ ` T ) $.
    isepic.2 $e |- H = ( hom ` T ) $.
    isepic.3 $e |- R = ( o_ ` T ) $.
    $( The predicate "is an epimorphism" when the morphism belongs to a
       homset.  (Contributed by FL, 27-Oct-2008.) $)
    isepic $p |- ( ( T e. Cat /\ ( A e. O /\ B e. O ) /\
      F e. ( H ` <. A , B >. ) ) -> ( F e. ( Epic ` T ) <->
        A. c e. O A. g e. ( H ` <. B , c >. ) A. h e. ( H ` <. B , c >. )
        ( ( g R F ) = ( h R F ) -> g = h ) ) ) $=
      ( wcel wa cfv w3a wceq wi wral ccat cop cepi cdom_ cdm cv ccod_ co isepib
      wb eqid 3adant2 wal ax-17 hbra1 hban hbral pm3.31 simpll1 simp2r ad2antrr
      ra42 simplr ishomd biimpd anim12d syl3anc simp1 anim12i a1i expcom eqcoms
      cehm eqtr 3ad2ant3 com12 imp adantl 3ad2ant2 adantr impcom 3jca ex 3expib
      syl6 3imp jcad syld imim1d com13 alrimi exp31 com23 alrimiv ralbii df-ral
      3syl r2al bitri sylibr iepiclem impbid bitrd ) DUANZAINZBINZOZGABUBHPZNZQ
      ZGDUCPNZGDUDPZUEZNZEUFZDUGPZPZFUFZXPPZRZXOXLPZGXPPZRZXRXLPZYBRZQZXOGCUHXR
      GCUHRXOXRRSZSZFXMTZEXMTZOZYGFBJUFZUBHPZTEYMTZJITZXDXIXKYKUJXGXHXPXLCDEFGX
      MXMUKZXLUKZXPUKZMUIULXJYKYOXJYKYOXJYKOZYLINZXOYMNZXRYMNZOZYGSZFUMZEUMZSZJ
      UMZYOYSUUGJXJYKUUGXJYTYKUUFXJYTYKUUFXJYTOZYKOZUUEEUUIYKEUUIEUNXNYJEXNEUNY
      IEXMUOUPUPUUJUUDFUUIYKFUUIFUNXNYJFXNFUNYIFEXMXOXMNZFUNYHFXMUOUQUPUPYKUUIU
      UDYJXNUUIUUDSZYJUUKXRXMNZOZYHSUUNYFOZYGSZXNUULSYHEFXMXMVBUUNYFYGURUUIXNUU
      PUUDUUIXNUUPUUDSUUIXNOZUUCUUOYGUUQUUCUUKYABRZXQYLRZQZUUMYDBRZXSYLRZQZOZUU
      OUUQXDXFYTUUCUVDSXDXGXIYTXNUSXJXFYTXNXDXEXFXIUTVAXJYTXNVCXDXFYTQZUUAUUTUU
      BUVCUVEUUAUUTBYLXPXLDXOHXMIKYPYQYRLVDVEUVEUUBUVCBYLXPXLDXRHXMIKYPYQYRLVDV
      EVFVGUUQUVDUUNYFUVDUUNSUUQUUTUUKUVCUUMUUKUURUUSVHUUMUVAUVBVHVIVJXJUVDYFSZ
      YTXNXDXGXIUVFXDXEXFXIUVFSXDXEXFQXIYBBRZUVFABXPDGHIKYRLVMUVGUVDYFUVGUVDOXT
      YCYEUVDXTUVGUUTUVCXTUUSUUKUVCXTSUURUVCUUSXTUVBUUMUUSXTSZUVAUVHYLXSUUSYLXS
      RXTXQYLXSVNVKVLVOVPVOVQVRUVDUVGYCUUTUVGYCSZUVCUURUUKUVIUUSUVGUURYCUURYCSB
      YBUURBYBRZYCYABYBVNVKVLVPVSVTWAUVDUVGYEUVCUVGYESZUUTUVAUUMUVKUVBUVGUVAYEU
      VAYESBYBUVAUVJYEYDBYBVNVKVLVPVSVRWAWBWCWEWDWFVAWGWHWIWCWJWQWAWAWKWKWLWMVQ
      WNYOUUFJITUUHYNUUFJIYGEFYMYMWRWOUUFJIWPWSWTWCABCDEFGHIJKLMXAXBXC $.
      $( [17-Nov-2008] $) $( [27-Oct-2008] $)
  $}

  ${
    $d f g x y T $.  $d f M $.
    isiso.1 $e |- M = dom ( dom_ ` T ) $.
    isiso.2 $e |- D = ( dom_ ` T ) $.
    isiso.3 $e |- C = ( cod_ ` T ) $.
    isiso.4 $e |- R = ( o_ ` T ) $.
    isiso.5 $e |- J = ( id_ ` T ) $.
    $( Isomorphisms of a category.  (Contributed by FL, 9-Jun-2014.) $)
    isiso $p |- ( T e. Cat -> ( Iso ` T ) = { f e. M | E. g e. M
      ( ( D ` g ) = ( C ` f ) /\
        ( C ` g ) = ( D ` f ) /\
        ( ( f R g ) = ( J ` ( D ` g ) ) /\ ( g R f ) = ( J ` ( D ` f ) ) )
      ) } ) $=
      ( wcel cfv wceq co cvv fveq1d eqeq12d vx vy ccat ciso cv cdom_ ccod_ cid_
      co_ wa w3a cdm wrex crab fvex a1i dmexg rabexg fveq2 dmeqd eleq2d fveq12d
      syl oveqd anbi12d 3anbi123d rexbidv2 rabeqbidv df-isoc mpdan eqcomi eqtrd
      fvopab4g ) DUCNZDUDOZFUEZDUFOZOZEUEZDUGOZOZPZVPVTOZVSVQOZPZVSVPDUIOZQZVRD
      UHOZOZPZVPVSWFQZWDWHOZPZUJZUKZFVQULZUMZEWPUNZVPBOZVSAOZPZVPAOZVSBOZPZVSVP
      CQZWSGOZPZVPVSCQZXCGOZPZUJZUKZFHUMZEHUNVNWRRNZVOWRPVNWPRNZXNVNVQRNZXOXPVN
      DUFUOUPVQRUQVCWQEWPRURVCUAUBDVPUAUEZUFOZOZVSXQUGOZOZPZVPXTOZVSXROZPZVSVPX
      QUIOZQZXSXQUHOZOZPZVPVSYFQZYDYHOZPZUJZUKZFXRULZUMZEYPUNWRUCRUDXQDPZYQWQEY
      PWPYRXRVQXQDUFUSZUTZYRYOWOFYPWPYRVPYPNVPWPNZYOWOYRYPWPVPYTVAYRYBWBYEWEYNW
      NYRXSVRYAWAYRVPXRVQYSSZYRVSXTVTXQDUGUSZSTYRYCWCYDWDYRVPXTVTUUCSYRVSXRVQYS
      SZTYRYJWJYMWMYRYGWGYIWIYRYFWFVSVPXQDUIUSZVDYRXSVRYHWHXQDUHUSZUUBVBTYRYKWK
      YLWLYRYFWFVPVSUUEVDYRYDWDYHWHUUFUUDVBTVEVFVEVGVHUAUBEFVIVMVJVNWQXMEWPHWPH
      PVNHWPIVKUPZVNWOXLFWPHVNUUAVPHNWOXLVNWPHVPUUGVAVNWBXAWEXDWNXKVNVRWSWAWTVN
      VPVQBVQBPVNBVQJVKUPZSZVNVSVTAVTAPVNAVTKVKUPZSTVNWCXBWDXCVNVPVTAUUJSVNVSVQ
      BUUHSZTVNWJXGWMXJVNWGXEWIXFVNWFCVSVPWFCPVNCWFLVKUPZVDVNVRWSWHGWHGPVNGWHMV
      KUPZUUIVBTVNWKXHWLXIVNWFCVPVSUULVDVNWDXCWHGUUMUUKVBTVEVFVEVGVHVL $.
      $( [27-Jun-2014] $)
  $}

  $c cinv $.
  $( Extend class notation to include a function that returns the inverses of a
     morphism. $)
  ccinv $a class cinv $.

  ${
    $d x y f z g $.
    $( Function ( indexed by the category ` x ` ) returning the inverses of a
       morphism ` f ` . $)
    df-cinv $a |- cinv = { <. x , y >. | ( x e. Cat /\
      y = { <. f , z >. | ( f e. dom ( dom_ ` x ) /\
      z = { g e. dom ( dom_ ` x ) | ( ( f ( o_ ` x ) g ) =
      ( ( id_ ` x ) ` ( ( cod_ ` x ) ` f ) ) /\
     ( g ( o_ ` x ) f ) = ( ( id_ ` x ) ` ( ( dom_ ` x ) ` f ) ) ) } ) } ) } $.
  $}

  ${
    $d C x y z $.  $d D x y z $.  $d J x y z $.  $d M f g x y z $.
    $d R x y z $.  $d T f g x y z $.
    cinvlem1.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem1.2 $e |- D = ( dom_ ` T ) $.
    cinvlem1.3 $e |- C = ( cod_ ` T ) $.
    cinvlem1.4 $e |- R = ( o_ ` T ) $.
    cinvlem1.5 $e |- J = ( id_ ` T ) $.
    $( The set of the inverses of all the morphisms .  (Contributed by FL,
       22-Dec-2008.) $)
    cinvlem1 $p |- ( T e. Cat -> ( cinv ` T ) = { <. f , z >. | ( f e. M /\
      z = { g e. M | ( ( f R g ) = ( J ` ( C ` f ) )
      /\ ( g R f ) = ( J ` ( D ` f ) ) ) } ) } ) $=
      ( cv cdom_ cfv co wceq syl6eqr vx vy cdm wcel co_ ccod_ cid_ wa crab ccat
      copab ccinv fveq2 dmeqd eleq2d fveq12d eqeq12d anbi12d rabeqbidv opabbidv
      oveqd fveq1d eqeq2d df-cinv cvv fvex dmex eqeltri opabex2 fvopab4 ) UAUBE
      FOZUAOZPQZUCZUDZAOZVKGOZVLUEQZRZVKVLUFQZQZVLUGQZQZSZVQVKVRRZVKVMQZWBQZSZU
      HZGVNUIZSZUHZFAUKVKIUDZVPVKVQDRZVKBQZHQZSZVQVKDRZVKCQZHQZSZUHZGIUIZSZUHZF
      AUKUJULVLESZWLXEFAXFVOWMWKXDXFVNIVKXFVNEPQZUCZIXFVMXGVLEPUMZUNJTZUOXFWJXC
      VPXFWIXBGVNIXJXFWDWQWHXAXFVSWNWCWPXFVRDVKVQXFVREUEQDVLEUEUMMTZVAXFWAWOWBH
      XFWBEUGQHVLEUGUMNTZXFVKVTBXFVTEUFQBVLEUFUMLTVBUPUQXFWEWRWGWTXFVRDVQVKXKVA
      XFWFWSWBHXLXFVKVMCXFVMXGCXIKTVBUPUQURUSVCURUTUAUBAFGVDFAIXCIXHVEJXGEPVFVG
      VHVIVJ $.
      $( [22-Dec-2008] $)
  $}

  ${
    $d C f z $.  $d D f z $.  $d F f g z $.  $d J f z $.  $d M f g z $.
    $d R f z $.  $d T f g z $.
    cinvlem2.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem2.2 $e |- D = ( dom_ ` T ) $.
    cinvlem2.3 $e |- C = ( cod_ ` T ) $.
    cinvlem2.4 $e |- R = ( o_ ` T ) $.
    cinvlem2.5 $e |- J = ( id_ ` T ) $.
    cinvlem2.6 $e |- T e. Cat $.
    $( The set of the inverses of the morphism ` F ` .  (Contributed by FL,
       22-Dec-2008.) $)
    cinvlem2 $p |- ( F e. M -> ( ( cinv ` T ) ` F ) =
      { g e. M | ( ( F R g ) = ( J ` ( C ` F ) )
      /\ ( g R F ) = ( J ` ( D ` F ) ) ) } ) $=
      ( vf vz cv cfv co wceq co_ wa crab ccinv eqcomi a1i eqidd oveq123d fveq2d
      id fveq2 eqeq12d anbi12d rabbidv ccat wcel copab cinvlem1 ax-mp cdom_ cdm
      eqid cvv fvex dmex eqeltri rabex fvopab4 ) OPFOQZEQZDUARZSZVIARZGRZTZVJVI
      VKSZVIBRZGRZTZUBZEHUCZFVJCSZFARZGRZTZVJFCSZFBRZGRZTZUBZEHUCHDUDRZVIFTZVTW
      JEHWLVOWEVSWIWLVLWBVNWDWLVIFVJVJVKCVKCTWLCVKLUEUFZWLUJZWLVJUGZUHWLVMWCGVI
      FAUKUIULWLVPWFVRWHWLVJVJVIFVKCWMWOWNUHWLVQWGGVIFBUKUIULUMUNDUOUPWKVIHUPPQ
      WATUBOPUQTNPABVKDOEGHIJKVKVBMURUSWJEHHDUTRZVAVCIWPDUTVDVEVFVGVH $.
      $( [22-Dec-2008] $)
  $}

  ${
    $d C g $.  $d D g $.  $d F g $.  $d G g $.  $d J g $.  $d M g $.  $d R g $.
    $d T g $.
    cinvlem3.1 $e |- M = dom ( dom_ ` T ) $.
    cinvlem3.2 $e |- D = ( dom_ ` T ) $.
    cinvlem3.3 $e |- C = ( cod_ ` T ) $.
    cinvlem3.4 $e |- R = ( o_ ` T ) $.
    cinvlem3.5 $e |- J = ( id_ ` T ) $.
    cinvlem3.6 $e |- T e. Cat $.
    cinvlem3.7 $e |- F e. M $.
    $( The set of the inverses of the morphism ` F ` .  (Contributed by FL,
       22-Dec-2008.) $)
    cinvlem3 $p |-
      ( G e. ( ( cinv ` T ) ` F ) <->
      ( G e. M /\ ( F R G ) = ( J ` ( C ` F ) )
      /\ ( G R F ) = ( J ` ( D ` F ) ) ) ) $=
      ( vg co cfv wceq wcel cv wa ccinv w3a oveq2 eqeq1d oveq1 anbi12d cinvlem2
      crab elrab ax-mp eleq2i 3anass 3bitr4i ) FEPUAZCQZEARGRZSZUPECQZEBRGRZSZU
      BZPHUJZTFHTZEFCQZURSZFECQZVASZUBZUBFEDUCRRZTVEVGVIUDVCVJPFHUPFSZUSVGVBVIV
      LUQVFURUPFECUEUFVLUTVHVAUPFECUGUFUHUKVKVDFEHTVKVDSOABCDPEGHIJKLMNUIULUMVE
      VGVIUNUO $.
      $( [22-Dec-2008] $)
  $}

