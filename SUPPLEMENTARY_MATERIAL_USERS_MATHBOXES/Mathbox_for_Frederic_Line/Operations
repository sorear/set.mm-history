$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d ch u $.  $d ph u x z $.  $d ph u y z $.  $d ps u $.
    ssoprab2g.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference of operation class abstraction subclass from implication.
       (Contributed by FL, 24-Jan-2010.) $)
    ssoprab2g $p |- ( ph -> { <. <. x , y >. , z >. | ps } C_
        { <. <. x , y >. , z >. | ch } ) $=
      ( vu cv cop wa wex copab copab2 anim2d 2eximdv ssopab2dv dfoprab2 3sstr4g
      wceq ) AHIDIEIJTZBKZELDLZHFMUACKZELDLZHFMBDEFNCDEFNAUCUEHFAUBUDDEABCUAGOP
      QBDEFHRCDEFHRS $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The domain of an operation class abstraction.  Compare ~ dmoprabss .
       (Contributed by FL, 24-Jan-2010.) $)
    dmoprabsss $p |- dom { <. <. x , y >. , z >. |
    ( <. x , y >. e. ( A X. B ) /\ ph ) } C_ ( A X. B ) $=
      ( cv cop cxp wcel wa copab2 cdm oprab4 dmeqi dmoprabss eqsstri ) BGZCGZHE
      FIZJAKBCDLZMREJSFJKAKBCDLZMTUAUBABCDEFNOABCDEFPQ $.
      $( [24-Jan-2010] $)
  $}

  $( Value returned by the operation ` G ` in terms of the value returned by
     the "super"-operation ` F ` .  (A version of ~ oprssov adapted to partial
     operations.)  (Contributed by FL, 5-Oct-2009.) $)
  oprssopvg $p |- ( ( Fun F /\ G C_ F /\ <. A , B >. e. dom G )
    -> ( A F B ) = ( A G B ) ) $=
    ( wfun wss cop cdm wcel w3a cres wceq fvres df-ov 3eqtr4g 3ad2ant3 funssres
    co cfv wa oveqd 3adant3 eqtr3d ) CEZDCFZABGZDHZIZJABCUGKZRZABCRZABDRZUHUDUJ
    UKLUEUHUFUISUFCSUJUKUFUGCMABUINABCNOPUDUEUJULLUHUDUETUIDABCDQUAUBUC $.
    $( [5-Oct-2009] $)

  ${
    $d A x y z $.  $d B z $.
    dmoprabss5.1 $e |- B e. C $.
    $( The domain of an operation class abstraction.  (A version of ~ dmoprabss
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dmoprabss6 $p |- ( Rel A ->
     dom { <. <. x , y >. , z >. | ( <. x , y >. e. A /\ z = B ) } = A ) $=
      ( wrel cv cop wcel wceq wa copab2 cdm wex copab dmoprab opabbii syl5eq
      19.42v simpl elexi isseti jctr impbii eqtri opabid2 ) DHZAIBIJDKZCIELZMZA
      BCNOULCPZABQZDULABCRUIUNUJABQZDUNUJUKCPZMZABQUOUMUQABUJUKCUASUQUJABUQUJUJ
      UPUBUJUPCEEFGUCUDUEUFSUGABDUHTT $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d A z $.  $d B x y z $.  $d R x y z $.
    $( Existence of an operation class abstraction.  (A version of ~ oprabex2g
       adapted to partial operations.)  (Contributed by FL, 18-Apr-2010.) $)
    oprabex2gpop $p |- ( ( R e. B /\ Rel R ) -> { <. <. x , y >. , z >.
      | ( <. x , y >. e. R /\ z = A ) } e. _V ) $=
      ( wcel wrel wa cv cop wceq copab2 cdm crn cxp wss cvv relssdmrn adantl
      sseld anim1d ssoprab2g oprab4 dmexg rnexg jca eqid oprabex2g syl syl5eqel
      adantr ssexg syl2anc ) FEGZFHZIZAJZBJZKZFGZCJDLZIZABCMZUTFNZFOZPZGZVBIZAB
      CMZQVJRGVDRGUQVCVIABCUQVAVHVBUQFVGUTUPFVGQUOFSTUAUBUCUQVJURVEGUSVFGIVBIAB
      CMZRVBABCVEVFUDUQVERGZVFRGZIZVKRGUOVNUPUOVLVMFEUEFEUFUGULABCVEVFDRRVKVKUH
      UIUJUKVDVJRUMUN $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d F w x y z $.  $d R w x y z $.
    $( Representation of an operation class abstraction in terms of its
       values.  (A version of ~ fnov adapted to partial operations.)
       (Contributed by FL, 18-Apr-2010.) $)
    fnovpop $p |- ( Rel R -> ( F Fn R <->
    F = { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ z = ( x F y ) ) } ) )
        $=
      ( vw wfn cv wcel cfv wceq wa copab wex wi eleq1 wb a1i syl6eqr eqeq2d cop
      wrel co copab2 dffn5 elrel biimpcd ancld 2eximdv syl5com ex biimpd eqcoms
      pm2.43d imp impbid1 anbi1d 19.41vv anass fveq2 df-ov anbi2d pm5.32i bitri
      exlimivv 2exbidv 3bitr2d opabbidv dfoprab2 syl5bb ) EDGEFHZDIZCHZVKEJZKZL
      ZFCMZKDUBZEAHZBHZUAZDIZVMVSVTEUCZKZLZABCUDZKFCDEUEVRVQWFEVRVQVKWAKZWELZBN
      ANZFCMWFVRVPWIFCVRVPWGWBLZBNANZVOLZWJVOLZBNANZWIVRVLWKVOVRVLWKVRVLWKVRVLV
      LWKOVRVLLWGBNANVLWKABVKDUFVLWGWJABVLWGWBWGVLWBVKWADPUGUHUIUJUKUNWJVLABWGW
      BVLWBVLOWAVKWAVKKWBVLWAVKDPULUMUOVEUPUQWNWLQVRWJVOABURRVRWMWHABWMWHQVRWMW
      GWBVOLZLWHWGWBVOUSWGWOWEWGVOWDWBWGVNWCVMWGVNWAEJWCVKWAEUTVSVTEVASTVBVCVDR
      VFVGVHWEABCFVISTVJ $.
      $( [18-Apr-2010] $)
  $}

  ${
    $d R w x y z $.  $d ph w $.  $d ps x y $.
    dfoprab4spop.1 $e |- ( <. x , y >. = w -> ( ph <-> ps ) ) $.
    $( Class abstraction for operations in terms of class abstraction of
       ordered pairs.  (A version of ~ dfoprab4s adapted to partial
       operations.)  (Contributed by FL, 18-Apr-2010.) $)
    dfoprab4spop $p |- ( Rel R ->
         { <. <. x , y >. , z >. | ( <. x , y >. e. R /\ ph ) }
       = { <. w , z >. | ( w e. R /\ ps ) } ) $=
      ( wrel cv cop wcel wa copab2 cvv copab wceq cin sylbi syl5bbr cxp anbi12d
      eleq1 dfoprab3s anass elin wss df-rel df-ss incom eqtr eleq2d mpan anbi1d
      wb opabbidv syl5eq ) GIZCJDJKZGLZAMZCDENFJZOOUAZLZVBGLZBMZMZFEPVFFEPVAVFC
      DEFUSVBQUTVEABUSVBGUCHUBUDURVGVFFEVGVDVEMZBMURVFVDVEBUEURVHVEBVHVBVCGRZLZ
      URVEVBVCGUFURGVCUGZVJVEUOZGUHVKGVCRZGQZVLGVCUIVIVMQZVNVLVCGUJVOVNMVIGVBVI
      VMGUKULUMSSTUNTUPUQ $.
      $( [18-Apr-2010] $)
  $}


