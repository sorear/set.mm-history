$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Groups and related structures
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d G x $.  $d U x $.  $d V x $.  $d X x $.
    $( If a magma has a left identity element and a right identity element,
       they are equal.  (Contributed by FL, 25-Sep-2011.) $)
    ridlideq $p |- ( ( U e. X /\ V e. X ) ->
      ( A. x e. X ( ( U G x ) = x /\ ( x G V ) = x )
        -> U = V ) ) $=
      ( cv co wceq wa wral wcel wi oveq2 id eqeq12d oveq1 anbi12d im2anan9 eqtr
      rcla4v ex eqcoms adantrd adantl imp syl6com anidms com12 ) BAFZCGZUIHZUID
      CGZUIHZIZAEJZBEKZDEKZIZBDHZUOURUSLURUOUOIBBCGZBHZBDCGZBHZIZVBDHZDDCGZDHZI
      ZIUSUPUOVDUQUOVHUNVDABEUIBHZUKVAUMVCVIUJUTUIBUIBBCMVINZOVIULVBUIBUIBDCPVJ
      OQTUNVHADEUIDHZUKVEUMVGVKUJVBUIDUIDBCMVKNZOVKULVFUIDUIDDCPVLOQTRVDVHUSVCV
      HUSLVAVCVEUSVGVEUSLBVBBVBHVEUSBVBDSUAUBUCUDUEUFUGUH $.
      $( [25-Sep-2011] $)
  $}

  ${
    $d G x $.  $d U x $.  $d V x $.  $d X x $.
    $( If a magma has a left zero element and a right zero element, they are
       equal.  (Contributed by FL, 25-Dec-2011.) $)
    rzrlzreq $p |- ( ( U e. X /\ V e. X ) ->
      ( A. x e. X ( ( U G x ) = U /\ ( x G V ) = V )
        -> U = V ) ) $=
      ( cv co wceq wa wral wcel wi oveq2 eqeq1d oveq1 anbi12d rcla4va ex com12
      eqtr eqcoms adantr adantl imp syl2an an4s expcom anidms ) BAFZCGZBHZUIDCG
      ZDHZIZAEJZBEKZDEKZIZBDHZUOURUSLURUOUOIUSUPUOUQUOUSUPUOIBBCGZBHZBDCGZDHZIZ
      VBBHZDDCGZDHZIZUSUQUOIUNVDABEUIBHZUKVAUMVCVIUJUTBUIBBCMNVIULVBDUIBDCONPQU
      NVHADEUIDHZUKVEUMVGVJUJVBBUIDBCMNVJULVFDUIDDCONPQVDVHUSVCVHUSLVAVHVCUSVEV
      CUSLZVGVKBVBBVBHVCUSBVBDTRUAUBSUCUDUEUFUGUHS $.
      $( [25-Dec-2011] $)
  $}


  ${
    $d G x y z $.  $d U x y z $.  $d X x y z $.
    mgmlion.1 $e |- X = dom dom G $.
    $( If a magma has a left identity element, it is onto.  (Contributed by FL,
       25-Sep-2011.) $)
    mgmlion $p |- ( ( G e. Magma /\ U e. X /\ A. x e. X ( U G x ) = x )
      -> G : ( X X. X ) -onto-> X ) $=
      ( vy vz cmagm wcel cv co wceq wral w3a cxp wf wrex wa eqeq2d rcla4ev syl6
      wfo ismgm ibi 3ad2ant1 simpllr simplr eqcoms adantl weq oveq2 syl2anc jca
      id ex oveq1 rexbidv ralimdva 3impia foov sylanbrc ) CHIZBDIZBAJZCKZVDLZAD
      MZNDDOZDCPZVDFJZGJZCKZLZGDQZFDQZADMZVHDCUBVBVCVIVGVBVIHCDEUCUDUEVBVCVGVPV
      BVCRZVFVOADVQVDDIZRZVFVCVDBVKCKZLZGDQZRZVOVSVFWCVSVFRZVCWBVBVCVRVFUFWDVRV
      DVELZWBVQVRVFUGVFWEVSWEVDVEWEUNUHUIWAWEGVDDGAUJVTVEVDVKVDBCUKSTULUMUOVNWB
      FBDVJBLZVMWAGDWFVLVTVDVJBVKCUPSUQTUAURUSFGADDDCUTVA $.
      $( [25-Sep-2011] $)
  $}

  $( ` RR ` is a group for addition.  (Contributed by FL, 22-Dec-2008.) $)
  rrisgrp $p |- ( + |` ( RR X. RR ) ) e. GrpOp $=
    ( caddc cgr wcel cxp cres wss csubgo cfv w3a readdsubgo issubgo mpbi simp2i
    cr ) ABCZANNDEZBCZPAFZPAGHCOQRIJAPKLM $.
    $( [22-Dec-2008] $)

  $( A way to express the domain of a group.  (Contributed by FL,
     9-Jan-2011.) $)
  dmrngrp $p |- ( G e. GrpOp -> dom G = ( ran G X. ran G ) ) $=
    ( cgr wcel crn cxp wfo wf cdm wceq eqid grpofo fof fdm 3syl ) ABCADZOEZOAFP
    OAGAHPIAOOJKPOALPOAMN $.
    $( [9-Jan-2011] $)

  ${
    $d G x y $.  $d X x y $.
    bsmgrli.1 $e |- X = ran G $.
    $( The base set of an operation with a right and left identity element is
       not empty.  (Contributed by FL, 18-Feb-2010.) $)
    bsmgrli $p |- ( G e. ( Magma i^i ExId ) -> X =/= (/) ) $=
      ( vy vx cmagm cexid cin wcel cv co wceq wa wral wrex c0 wne isexid2 rexn0
      syl ) AFGHIDJZEJZAKUBLUBUAAKUBLMEBNZDBOBPQEDABCRUCDBST $.
      $( [23-Dec-2013] $) $( [18-Feb-2010] $)
  $}

  ${
    $d A x y z $.  $d B y z $.  $d C z $.  $d G x y z $.  $d X x y z $.
    smgrpass2.1 $e |- X = dom dom G $.
    $( A semi-group is associative.  (Contributed by FL, 12-Dec-2009.) $)
    smgrpass2 $p |- ( ( G e. SemiGrp /\ ( A e. X /\ B e. X /\ C e. X ) )
      -> ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( vx vy vz csem wcel cv co wceq wral oveq1 oveq1d eqeq12d oveq2 oveq2d
      w3a smgrpass rcla43v mpan9 ) DJKGLZHLZDMZILZDMZUEUFUHDMZDMZNZIEOHEOGEOAEK
      BEKCEKUAABDMZCDMZABCDMZDMZNZGHIDEFUBULUQAUFDMZUHDMZAUJDMZNUMUHDMZABUHDMZD
      MZNGHIABCEEEUEANZUIUSUKUTVDUGURUHDUEAUFDPQUEAUJDPRUFBNZUSVAUTVCVEURUMUHDU
      FBADSQVEUJVBADUFBUHDPTRUHCNZVAUNVCUPUHCUMDSVFVBUOADUHCBDSTRUCUD $.
      $( [12-Dec-2009] $)
  $}

  ${
    $d G x y $.
    $( An abelian group is a commutative group.  (Contributed by FL,
       14-Sep-2010.) $)
    ablocomgrp $p |- ( G e. AbelOp -> G e. ( GrpOp i^i Com1 ) ) $=
      ( vx vy cgr wcel cv co wceq crn wral wa ccm1 cablo cin cdm grporndm iscom
      wi eqid biimprd raleq raleqbi1dv imbi1d syl5ibr mpcom isablo elin 3imtr4i
      imdistani ) ADEZBFZCFZAGULUKAGHZCAIZJZBUNJZKUJALEZKAMEADLNEUJUPUQUNAOOZHZ
      UJUPUQRZAPUJUTUSUMCURJZBURJZUQRUJUQVBBCDAURURSQTUSUPVBUQUOVABUNURUMCUNURU
      AUBUCUDUEUIBCAUNUNSUFADLUGUH $.
      $( [14-Sep-2010] $)
  $}


  ${
    reacomsmgrp1.1 $e |- X = dom dom G $.
    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp1 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( B G ( A G C ) ) ) $=
      ( csem ccm1 cin wcel w3a wa wceq inss1 sseli smgrpass2 eqcomd sylan inss2
      co adantr simpr1 simpr2 iscomb syl3anc oveq1d simp2 simp1 syl2an 3eqtrd
      simp3 3jca ) DGHIZJZAEJZBEJZCEJZKZLZABCDTDTZABDTZCDTZBADTZCDTZBACDTDTZUND
      GJZURUTVBMUMGDGHNOZVFURLVBUTABCDEFPQRUSVAVCCDUSDHJZUOUPVAVCMUNVHURUMHDGHS
      OUAUNUOUPUQUBUNUOUPUQUCABDEFUDUEUFUNVFUPUOUQKVDVEMURVGURUPUOUQUOUPUQUGUOU
      PUQUHUOUPUQUKULBACDEFPUIUJ $.
      $( [14-Sep-2010] $)


    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp2 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( C G ( B G A ) ) ) $=
      ( csem ccm1 cin wcel w3a wa co wceq inss1 smgrpass2 eqcomd iscomb syl3anc
      sseli sylan inss2 adantr simpr1 simpr2 oveq1d cmagm smgrpismgm clmgm 3exp
      syl wi com13 imp 3adant3 mpan9 simpr3 3eqtrd ) DGHIZJZAEJZBEJZCEJZKZLZABC
      DMDMZABDMZCDMZBADMZCDMZCVIDMZUTDGJZVDVFVHNUSGDGHOTZVLVDLVHVFABCDEFPQUAVEV
      GVICDVEDHJZVAVBVGVINUTVNVDUSHDGHUBTUCZUTVAVBVCUDUTVAVBVCUEABDEFRSUFVEVNVI
      EJZVCVJVKNVOUTDUGJZVDVPUTVLVQVMDUHUKVAVBVQVPULZVCVAVBVRVQVBVAVPVQVBVAVPBA
      DEFUIUJUMUNUOUPUTVAVBVCUQVICDEFRSUR $.
      $( [14-Sep-2010] $)


    $( Rearrangement of three terms in a commutative semi-group.  (Contributed
       by FL, 14-Sep-2010.) $)
    reacomsmgrp3 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( csem ccm1 cin wcel w3a wa wceq inss2 sseli adantr clmgm syl3anc iscomb
      co cmagm inss1 smgrpismgm syl simpr1 simpr2 simpr3 simp3 simp1 simp2 3jca
      reacomsmgrp2 sylan2 3eqtrd ) DGHIZJZAEJZBEJZCEJZKZLZABDTZCDTZCVBDTZBACDTZ
      DTZVEBDTZVADHJZVBEJZUSVCVDMUPVHUTUOHDGHNOPZVADUAJZUQURVIUPVKUTUPDGJVKUOGD
      GHUBODUCUDPZUPUQURUSUEZUPUQURUSUFZABDEFQRUPUQURUSUGZVBCDEFSRUTUPUSUQURKVD
      VFMUTUSUQURUQURUSUHUQURUSUIUQURUSUJUKCABDEFULUMVAVHURVEEJZVFVGMVJVNVAVKUQ
      USVPVLVMVOACDEFQRBVEDEFSRUN $.
      $( [14-Sep-2010] $)


    $( Rearrangement of terms in a commutative semi-group.  (Contributed by FL,
       18-Sep-2010.) $)
    reacomsmgrp4 $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
      ( A e. X /\ B e. X /\ C e. X ) )
        -> ( A G ( B G C ) ) = ( ( C G B ) G A ) ) $=
      ( csem ccm1 cin wcel w3a wa co reacomsmgrp2 inss1 sseli simp3 simp2 simp1
      wceq 3jca smgrpass2 eqcomd syl2an eqtrd ) DGHIZJZAEJZBEJZCEJZKZLABCDMDMCB
      ADMDMZCBDMADMZABCDEFNUGDGJZUJUIUHKZULUMTUKUFGDGHOPUKUJUIUHUHUIUJQUHUIUJRU
      HUIUJSUAUNUOLUMULCBADEFUBUCUDUE $.
      $( [18-Sep-2010] $)

  $}

  ${
    resgcom.1 $e |- X = dom dom G $.
    $( Rearrangement of four terms in a commutative, associative magma.
       (Contributed by FL, 14-Sep-2010.) $)
    resgcom $p |- ( ( G e. ( SemiGrp i^i Com1 ) /\
  ( ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) ) )
   -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( csem ccm1 wcel wa co wceq wi clmgm 3exp imp adantl com12 smgrpass2 3syl
      inss1 sseli adantr simprll simprlr cmagm smgrpismgm com3l syl13anc simplr
      cin w3a simprl simprr 3jca reacomsmgrp1 sylan2 oveq2d simprrl com13 mpan9
      syl eqcomd 3eqtrd ) EHIULZJZAFJZBFJZKZCFJZDFJZKZKZKZABELCDELZELZABVPELZEL
      ZACBDELZELZELZACELVTELZVOEHJZVHVIVPFJZVQVSMVGWDVNVFHEHIUBUCZUDZVGVHVIVMUE
      ZVGVHVIVMUFVGVNWEVGWDEUGJZVNWENWFEUHZVNWIWEVMWIWENZVJVKVLWKWIVKVLWEWIVKVL
      WECDEFGOPUIQRSUAQABVPEFGTUJVOVRWAAEVNVGVIVKVLUMVRWAMVNVIVKVLVHVIVMUKVJVKV
      LUNVJVKVLUOUPBCDEFGUQURUSVOWDVHVKVTFJZWBWCMWGWHVGVJVKVLUTVGWIVNWLVGWDWIWF
      WJVCVJVMWIWLNZVIVMWMNVHVMVIWMVLVIWMNVKWIVIVLWLWIVIVLWLBDEFGOPVARSRQVBWDVH
      VKWLUMKWCWBACVTEFGTVDUJVE $.
      $( [14-Sep-2010] $)
  $}

  ${
    ablinvop.1 $e |- X = ran G $.
    ablinvop.2 $e |- N = ( inv ` G ) $.
    $( The inverse of the abelian group operation doesn't reverse the
       arguments. cf ~ grpoinvop .  (Contributed by FL, 14-Sep-2010.) $)
    abloinvop $p |- ( ( G e. AbelOp /\ A e. X /\ B e. X ) ->
                   ( N ` ( A G B ) ) = ( ( N ` A ) G ( N ` B ) ) ) $=
      ( cablo wcel w3a co cfv cgr wceq ablogrpo grpoinvop syl3an1 simp1 3adant2
      grpoinvcl 3adant3 ablocom syl3anc eqtrd ) CHIZAEIZBEIZJZABCKDLZBDLZADLZCK
      ZUKUJCKZUECMIZUFUGUIULNCOZABCDEFGPQUHUEUJEIZUKEIZULUMNUEUFUGRUEUNUFUGUPUO
      UNUGUPUFBCDEFGTSQUEUNUFUGUQUOUNUFUQUGACDEFGTUAQUJUKCEFUBUCUD $.
      $( [14-Sep-2010] $)
  $}

  ${
    $d x y z F $.  $d x y z G $.  $d x y z M $.  $d x y z N $.  $d x y z X $.
    $d x y z Z $.
    isppm.1 $e |- X = dom dom G $.
    isppm.2 $e |- Z = ( ZZ>= ` M ) $.
    $( The sequence of partial composites of elements of a magma is a sequence
       of elements of this magma.  (Contributed by FL, 24-Jan-2010.) $)
    isppm $p |- ( ( G e. Magma /\ M e. ZZ /\ F : Z --> X ) ->
      seq M ( G , F ) : Z --> X ) $=
      ( vx vy cmagm wcel cz wf w3a simp2 cv cfv ffvelrn 3ad2antl3 wa clmgm seqf
      co 3expb 3ad2antl1 ) BJKZCLKZEDAMZNHIBDACEGUFUGUHOUHUFHPZEKUIAQDKUGEDUIAR
      SUFUGUIDKZIPZDKZTUIUKBUCDKZUHUFUJULUMUIUKBDFUAUDUEUB $.
      $( [30-May-2014] $) $( [24-Jan-2010] $)

    seqzp2.1 $e |- G e. SemiGrp $.
    seqzp2.2 $e |- F : Z --> X $.
    $( Value of the arbitrary-based recursive sequence builder at a successor
       value when the operation ` G ` is associative.  Compare with ~ seqp1 .
       (Contributed by FL, 24-Jan-2010.) $)
    seqzp2 $p |- ( N e. Z ->
      ( seq M ( G , F ) ` ( N + 1 ) ) =
        ( ( F ` M ) G ( seq ( M + 1 ) ( G , F ) ` ( N + 1 ) ) ) ) $=
      ( vx vy vz wcel c1 caddc co cv adantl cfv wa cmagm smgrpismgm ax-mp clmgm
      csem mp3an1 w3a wceq smgrpass2 mpan cuz eluzp1p1 eleq2s cz eluzel2 elfzuz
      cfz syl6eleqr ffvelrni syl seq1p ) DFNZKLMBEACDOPQZKRZENZLRZENZUAVEVGBQZE
      NZVCBUBNZVFVHVJBUFNZVKIBUCUDVEVGBEGUEUGSVFVHMRZENUHZVIVMBQVEVGVMBQBQUIZVC
      VLVNVOIVEVGVMBEGUJUKSVDCOPQULTNDCULTZFCDUMHUNCUONDVPFCDUPHUNVECVDURQNZVEA
      TENZVCVQVEFNVRVQVEVPFVECVDUQHUSFEVEAJUTVASVB $.
      $( [24-Jan-2010] $)
  $}

  $( A monoid is associative.  (Contributed by FL, 2-Nov-2009.) $)
  mndisass $p |- ( G e. Mnd -> G e. Ass ) $=
    ( cmnd wcel csem cass mndissmgrp smgrpisass syl ) ABCADCAECAFAGH $.
    $( [2-Nov-2009] $)

  ${
    $d G x y z $.  $d X x y z $.
    mndid.1 $e |- X = ran G $.
    $( A monoid has an identity element.  (Contributed by FL, 2-Nov-2009.) $)
    mndid $p |- ( G e. Mnd -> E. x e. X A. y e. X
      ( ( x G y ) = y /\ ( y G x ) = y ) ) $=
      ( vz cmnd wcel cv co wceq wa wral wrex cxp wf w3a ismnd2 simp3 syl6bi
      pm2.43i ) CGHZAIZBIZCJZUDKUDUCCJUDKLBDMADNZUBUBDDODCPZUEFIZCJUCUDUHCJCJKF
      DMBDMADMZUFQUFABFGCDERUGUIUFSTUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $( A monoid is an internal operation.  (Contributed by FL, 2-Nov-2009.) $)
    mndio $p |- ( G e. Mnd -> G : ( X X. X ) --> X ) $=
      ( vx vy vz cmnd wcel cxp wf cv co wceq wral wrex w3a ismnd2 simp1 syl6bi
      wa pm2.43i ) AGHZBBIBAJZUBUBUCDKZEKZALZFKZALUDUEUGALALMFBNEBNDBNZUFUEMUEU
      DALUEMTEBNDBOZPUCDEFGABCQUCUHUIRSUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $( A monoid is associative.  (Contributed by FL, 2-Nov-2009.) $)
    mndass $p |- ( G e. Mnd ->
        A. x e. X A. y e. X A. z e. X
          ( ( x G y ) G z ) = ( x G ( y G z ) ) ) $=
      ( cmnd wcel cv co wceq wral cxp wf wa wrex w3a ismnd2 simp2 syl6bi
      pm2.43i ) DGHZAIZBIZDJZCIZDJUCUDUFDJDJKCELBELAELZUBUBEEMEDNZUGUEUDKUDUCDJ
      UDKOBELAEPZQUGABCGDEFRUHUGUISTUA $.
      $( [23-Dec-2013] $) $( [2-Nov-2009] $)

    $d A x y z $.  $d B y z $.  $d C z $.
    $( A monoid is associative.  (Contributed by FL, 12-Dec-2009.) $)
    mndass2 $p |- ( ( G e. Mnd /\ ( A e. X /\ B e. X /\ C e. X ) )
      -> ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( vx vy vz cmnd wcel cv co wceq wral oveq1 oveq1d eqeq12d oveq2 oveq2d
      w3a mndass rcla43v mpan9 ) DJKGLZHLZDMZILZDMZUEUFUHDMZDMZNZIEOHEOGEOAEKBE
      KCEKUAABDMZCDMZABCDMZDMZNZGHIDEFUBULUQAUFDMZUHDMZAUJDMZNUMUHDMZABUHDMZDMZ
      NGHIABCEEEUEANZUIUSUKUTVDUGURUHDUEAUFDPQUEAUJDPRUFBNZUSVAUTVCVEURUMUHDUFB
      ADSQVEUJVBADUFBUHDPTRUHCNZVAUNVCUPUHCUMDSVFVBUOADUHCBDSTRUCUD $.
      $( [12-Dec-2009] $)
  $}

  $( Declare the symbol for the monoid exponentiation. $)
  $c ^md $.

  $( Extend class notation with monoid exponentiation. $)
  clsg $a class ^md $.

  ${
    $d g f x y z $.
    $( Define the exponentiation of an element of a monoid.  Experimental.  I
       define exponentiation on a monoid (and not on a semi-group or a magma )
       because I need an identity element for the basis hypothesis and
       associativity for interesting properties such as the composite of two
       exponentiated elements. ` ZZ ` is used in ~ df-gx here I used ` NN0 `
       because the inverse is not defined in a monoid. $)
    df-expsg $a |- ^md = { <. g , f >. | ( g e. Mnd /\
     f = { <. <. x , y >. , z >. | ( ( x e. ran g /\ y e. NN0 ) /\
       z = if ( y = 0 , ( Id ` g ) ,
         ( seq 1 ( g , ( NN x. { x } ) ) ` y ) ) ) } ) } $.
  $}

  ${
    expmiz.2 $e |- F =
        ( rec ( { <. a , b >. | b = ( a G A ) } , ( Id ` G ) ) |` om ) $.
    ${
      $( Value of a member of a monoid (or any other structure where ` Id ` is
         defined ) raised to the 0th power.  (Contributed by FL,
         12-Dec-2009.) $)
      expmiz $p |- ( F ` (/) ) = ( Id ` G ) $=
        ( c0 cfv cv co wceq copab cgi crdg com cres fveq1i cvv wcel fvex ax-mp
        fr0g eqtri ) GBHGEIDIACJKDELZCMHZNOPZHZUEGBUFFQUERSUGUEKCMTUERUDUBUAUC
        $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d A a b $.  $d F a b $.  $d G a b $.  $d N a b $.
      $( Exponentiation of a monoid.  The value at a successor.  What I am
         calculating is ` ( A ( ^md `` G ) N ) ` .  (Contributed by FL,
         12-Dec-2009.) $)
      expm $p |- ( N e. om ->
      ( F ` suc N ) = ( ( F ` N ) G A ) ) $=
        ( com wcel csuc cv co wceq copab cgi cfv crdg cres frsuc fveq1i 3eqtr3g
        eqcomi fveq2i fvex ovex oveq1 fvopab eqtr3i ) DHIDJZFKEKZACLZMEFNZCOPZQ
        HRZPZDUNPZULPZUIBPZDBPZACLZUMDULSURUOUIBUNGTUBUSULPUQUTUSUPULDBUNGTUCEF
        USUKUTDBUDUSACUEUJUSACUFUGUHUA $.
        $( [12-Dec-2009] $)
    $}

    expus.1 $e |- G e. Mnd $.
    expus.3 $e |- A e. X $.
    expus.4 $e |- X = ran G $.
    ${
      $d A a b $.  $d F a b y $.  $d F x y $.  $d G a b $.  $d X x y $.
      $( The exponentiation of a member of a monoid belongs to the underlying
         set.  (Contributed by FL, 12-Dec-2009.) $)
      expus $p |- ( x e. om -> ( F ` x ) e. X ) $=
        ( vy cv cfv wcel c0 wceq fveq2 eleq1d ax-mp csuc cmagm cexid cin expmiz
        cmnd mndmgmid iorlid com wa co adantr cxp wf mndio fovrn mp3an13 adantl
        expm eqeltrd ex finds1 ) AMZCNZEOPCNZEOZLMZCNZEOZVGUAZCNZEOZALVCPQVDVEE
        VCPCRSVCVGQVDVHEVCVGCRSVCVJQVDVKEVCVJCRSDUBUCUDOZVFDUFOZVMIDUGTVEDEKBCD
        FGHUEUHTVGUIOZVIVLVOVIUJVKVHBDUKZEVOVKVPQVIBCDVGFGHUSULVIVPEOZVOEEUMEDU
        NZVIBEOVQVNVRIDEKUOTJVHBEEEDUPUQURUTVAVB $.
        $( [12-Dec-2009] $)
    $}

  $}

  $( The range of the domain of a magma equals the domain of the domain.
     (Contributed by FL, 17-May-2010.) $)
  mgmrddd $p |- ( G e. Magma -> ran dom G = dom dom G ) $=
    ( cmagm wcel cdm crn wceq cxp eqid ismgm fdm rneq rnxpid syl6eq syl pm2.43i
    wf syl6bi ) ABCZADZEZSDZFZRRUAUAGZUAAPZUBBAUAUAHIUDSUCFZUBUCUAAJUETUCEUASUC
    KUALMNQO $.
    $( [17-May-2010] $)

  ${
    unsgrp.1 $e |- X = ran G $.
    $( The underlying set of a group is a set.  (Contributed by FL,
       17-May-2010.) $)
    unsgrp $p |- ( G e. GrpOp -> X e. _V ) $=
      ( cgr wcel crn cvv rnexg syl5eqel ) ADEBAFGCADHI $.
      $( [17-May-2010] $)
  $}

  ${
    $d G x y z $.
    $( A group operation is a left group action of the group on itself.
       (Contributed by FL, 17-May-2010.) $)
    ltlga $p |- ( G e. GrpOp -> <. G , G >. e. GrpAct ) $=
      ( vx vy vz cgr wcel cop crn cdm wf cv co wceq wral wa eqid wi 3syl eqtr4d
      cxp cga cgi cfv isga id cmnd grpomnd mndio grporndm cmagm mndismgm xpeq2d
      mgmrddd feq23d biimpcd pm2.43i eleq2d grpolid 3exp2 com23 com24 ralrimivv
      grpoass imp4b jca ex sylbid ralrimiv mpbir3and ) AEFZAAGUAFVJAHZAIZHZTZVM
      AJZAUBUCZBKZALVQMZCKZDKZALVQALVSVTVQALALMZDVKNCVKNZOZBVMNBCDEVPAAVKVMVKPZ
      VMPVPPZUDVJUEVJVOVJAUFFZVKVKTZVKAJZVJVOQAUGZAVKWDUHVJWHVOVJWGVKVNVMAVJVKV
      MVKVJVKVLIZVMAUIZVJWFAUJFVMWJMWIAUKAUMRZSZULWMUNUORUPVJWCBVMVJVQVMFVQVKFZ
      WCVJVMVKVQVJVMWJVKWLWKSUQVJWNWCVJWNOZVRWBVQVPAVKWDWEURWOWACDVKVKVJWNVSVKF
      ZVTVKFZWAVJWQWPWNWAVJWPWQWNWAQVJWPWQWNWAVSVTVQAVKWDVCUSUTVAVDVBVEVFVGVHVI
      $.
      $( [17-May-2010] $)
  $}


  ${
    $d A f g $.  $d A g x $.  $d P f g $.
    symgfo.1 $e |- A e. _V $.
    symgfo.2 $e |- P = { x | x : A -1-1-onto-> A } $.
    $( The operation of a symetry group is onto.  (Contributed by FL,
       17-May-2010.) $)
    symgfo $p |- ( SymGrp ` A ) : ( P X. P ) -onto-> P $=
      ( vf vg cmagm cexid wcel wf cgr cdm wceq ax-mp mpbir cv co wa wf1o cfv wb
      csymg cin cxp wfo elin symgf symggrpi fdm dmeq dmxpid expcom eqcoms com12
      eqtr 3syl ismgm wral wrex cid cres cab f1oi eqid elsymgrn eleqtrri symgov
      ccom f1of fcoi2 syl sylbi eleq2s adantl eqtrd mpan fcoi1 adantr mpan2 jca
      wi rgen oveq1 eqeq1d oveq2 anbi12d ralbidv rcla4ev isexid mpbir2an opidon
      mp2an ) BUCUAZHIUDJZCCUEZCWNUFWOWNHJZWNIJZWNHIUGWQWPCWNKZABCDEUHZWNLJZWQW
      SUBBDUIZLWNCWSCWNMZMZNZWTWSXCWPNXDWPMZNZXEWPCWNUJXCWPUKXFCNXGXEWBZCULXHCX
      FXGCXFNZXEXIXEWBXFXDXIXFXDNXECXFXDUPUMUNUOUNOUQOZUROPWRFQZGQZWNRZXLNZXLXK
      WNRZXLNZSZGCUSZFCUTZVABVBZCJZXTXLWNRZXLNZXLXTWNRZXLNZSZGCUSZXSXTBBAQTAVCZ
      CXTYHJBBXTTBVDABYHXTDYHVEZVFPEVGZYFGCXLCJZYCYEYAYKYCYJYAYKSYBXTXLVIZXLABC
      XTXLDEVHYKYLXLNZYAYMXLYHCXLYHJZBBXLTZYMABYHXLDYIVFZYOBBXLKZYMBBXLVJZBBXLV
      KVLVMEVNVOVPVQYKYAYEYJYKYASYDXLXTVIZXLABCXLXTDEVHYKYSXLNZYAYTXLYHCYNYOYTY
      PYOYQYTYRBBXLVRVLVMEVNVSVPVTWAWCXRYGFXTCXKXTNZXQYFGCUUAXNYCXPYEUUAXMYBXLX
      KXTXLWNWDWEUUAXOYDXLXKXTXLWNWFWEWGWHWIWMXAWRXSUBXBFGLWNCXJWJOPWKWNCXJWLO
      $.
      $( [23-Dec-2013] $) $( [17-May-2010] $)
  $}

  ${
    gaplc.1 $e |- X = ran G $.
    gaplc.2 $e |- Y = ran M $.
    $( The action of a particular group element is left-cancelable.
       (Contributed by FL, 17-May-2010.) $)
    gaplc $p |- ( ( M e. N /\ <. G , M >. e. GrpAct /\
                  ( C e. X /\ A e. Y /\ B e. Y ) )
      -> ( ( C M A ) = ( C M B ) -> A = B ) ) $=
      ( wcel w3a cvv cxp wf1o wceq wi wa cfv 3ad2ant3 cop cga crn c2nd csn cres
      ccnv ccom co eleq2i biimpi 3ad2ant1 eqid gapm syl3an3 ccur1 wne wfn gagrp
      c0 cgr unsgrp rnexg syl5eqel adantr anim12i mpancom 3adant3 ne0i 3ad2ant2
      wf gaf xpeq1 eqcomd ax-mp xpeq2 eqtri ffn fneq2 biimpd mpsyl syl jca 3jca
      3simpa 3simpb valvalcurfn eqeqan12d simp31 valcurfn1 f1oeq1 f1oeq2 f1oeq3
      eqcom syl121anc bitrd f1fveq expcom 3adant1 syl5com syl6bi eqcoms syl6bir
      wf1 f1of1 com23 mpcom imp syl9r mpd mpdan ) EFKZDEUAUBKZCGKZAHKZBHKZLZLZE
      UCZXSEUDCUEMNUFUGUHZOZCAEUIZCBEUIZPZABPZQZXQXLXMCDUCZKZYAXNXOYHXPXNYHGYGC
      IUJUKULCFXTDEYGXSYGUMZXSUMZXTUMZUNUOXRYARZACEUPSSZSZYBPZBYMSZYCPZRZYFYLGM
      KZHMKZRZHUTUQZEGHNZURZRZXNXORZLZUUAUUEXNXPRZLZRZYRXRUUJYAXRUUGUUIXRUUAUUE
      UUFXLXMUUAXQDVAKZXLXMRZUUAFDEUSUUKYSUULYTDGIVBXLYTXMXLHXSMJEFVCVDVEVFVGVH
      ZXRUUBUUDXQXLUUBXMXOXNUUBXPHAVIVJTZXLXMUUDXQUULYGXSNZXSEVKZUUDFDEYGXSYIYJ
      VLUUOUUCPZUUPEUUOURZUUDUUOGXSNZUUCGYGPZUUOUUSPIUUTUUSUUOGYGXSVMVNVOHXSPZU
      USUUCPJUVAUUCUUSHXSGVPVNVOVQUUOXSEVRUUQUURUUDUUOUUCEVSVTWAWBVHZWCZXQXLUUF
      XMXNXOXPWETWDXRUUAUUEUUHUUMUVCXQXLUUHXMXNXOXPWFTWDWCVEUUGYOUUIYQGHMMAECWG
      GHMMBECWGVFWBYRYDYNYPPZYLYEYRYDUVDYOYQYBYNYCYPYOYBYNPYNYBWNUKYQYCYPPYPYCW
      NUKWHVTXRYAUVDYEQZYMXTPZXRYAUVEQXRUUAUUBUUDXNUVFUUMUUNUVBXLXMXNXOXPWIGHMM
      EXTCYKWJWOUVFYAXRUVEUVFYAXSXSYMOZXRUVEQZXSXSYMXTWKUVAUVGUVHQZJUVIXSHXSHPZ
      UVGHHYMOZUVHUVJUVGHXSYMOUVKXSHXSYMWLXSHHYMWMWPUVKHHYMXDZXRUVEHHYMXEXQXLUV
      LUVEQZXMXOXPUVMXNUVLXOXPRZUVEUVLUVNRUVDYEHHABYMWQVTWRWSTWTXAXBVOXCXFXGXHX
      IXJXK $.
      $( [17-May-2010] $)
  $}

  ${
    gapm2.1 $e |- X = ran G $.
    gapm2.2 $e |- Y = ran M $.
    ${
      $( The action of a particular group element is a permutation of the base
         set. ~ gapm expressed with the currying operator.  (Contributed by FL,
         17-May-2010.) $)
      gapm2 $p |-
        ( ( M e. B /\ <. G , M >. e. GrpAct /\ ( A e. X /\ Y =/= (/) ) ) ->
        ( ( cur1 ` M ) ` A ) : Y -1-1-onto-> Y ) $=
        ( wcel cop cga c0 wa cfv cvv cxp wceq wf1o syl 3adant3 wne w3a c2nd csn
        ccur1 cres ccnv wfn cgr gagrp unsgrp crn rnexg syl5eqel 3ad2ant1 simp3r
        ccom wf gaf ffn simp3l eqid valcurfn1 syl221anc gapm 3adant3r wi f1oeq1
        biimpd eqcoms sylc ) DBIZCDJKIZAEIZFLUAZMZUBZADUENNZDUCAUDOPUFUGUQZQZFF
        VSRZFFVRRZVQEOIZFOIZVODEFPZUHZVNVTVLVMWCVPVLVMMZCUIIWCBCDUJCEGUKSTVLVMW
        DVPVLFDULOHDBUMUNUOVLVMVNVOUPVLVMWFVPWGWEFDURWFBCDEFGHUSWEFDUTSTVLVMVNV
        OVAEFOODVSAVSVBZVCVDVLVMVNWAVOABVSCDEFGHWHVEVFWAWBVGVSVRVSVRQWAWBFFVSVR
        VHVIVJVK $.
        $( [17-May-2010] $)
    $}

    ${
      $( The range of the action of a particular group element equals the range
         of the action.  (Contributed by FL, 17-May-2010.) $)
      rngapm $p |- (
        ( M e. B /\ <. G , M >. e. GrpAct /\ ( A e. X /\ Y =/= (/) ) )
        -> ran ( ( cur1 ` M ) ` A ) = ran M ) $=
        ( wcel cop cga c0 wne wa w3a ccur1 cfv wf1o crn wceq gapm2 f1ofo eqeq2i
        wfo forn biimpi 3syl syl ) DBICDJKIAEIFLMNOFFADPQQZRZUISZDSZTZABCDEFGHU
        AUJFFUIUDUKFTZUMFFUIUBFFUIUEUNUMFULUKHUCUFUGUH $.
        $( [17-May-2010] $)
    $}
  $}

  ${
    $( The proof has been designed so that B and x needn't be distinct. $)
    $d A x y $.  $d B y $.  $d C x y $.  $d F t $.  $d G t $.  $d H t x $.
    $d H x y $.
    fnopabco2b.2 $e |- F = { <. x , y >. | ( x e. A /\ y = B ) } $.
    fnopabco2b. $e |- G = { <. x , y >. | ( x e. A /\ y = ( H ` B ) ) } $.
    $( Composition of a function with a function abstraction.  Adapted from
       ~ fnopabco .  (Contributed by FL, 17-May-2010.) $)
    fnopabco2b $p |- ( ( A. x e. A B e. C /\ H Fn C )
        -> G = ( H o. F ) ) $=
      ( vt wcel wral wfn wa wceq cv cfv wi cvv ccom hbra1 ax-17 hban ra4 eqcomi
      copab fveq1i fvopab2 syl5eqr ancoms 3adant3 fveq2d 3adant2r wfun 3ad2ant3
      w3a fnfun elex ralimi fnopab2g sylib adantl 3ad2ant2 simp2l fvco2 syl3anc
      simpl fvex jctir syl syl5eq 3eqtr4rd syl6com imp pm2.43i ex com3l ralrimi
      3exp wb fnopab2 crn wss simpr adantr wf fopab2 biimpi frn hbopab1 hbxfreq
      fnco hbco eqfnfv2f sylancr mpbird ) DELZACMZHENZOZGHFUAZPZAQZGRZXDXBRZPZA
      CMZXAXGACWSWTAWRACUBWTAUCUDWSWTXDCLZXGSXIWSWTXGXIWSWTXGSZXIWSOZXJXIWSXKXJ
      SZWSXIWRXLWRACUEWRXKWTXGWRXKWTUQZXDFRZHRZDHRZXFXEWRXIWTXOXPPWSWRXIWTUQXND
      HWRXIXNDPZWTXIWRXQXIWROXNXDXIBQZDPOZABUGZRDXDXTFFXTIUFUHABCDEUIUJUKULUMUN
      XMHUOZFCNZXIXFXOPWTWRYAXKEHURUPXKWRYBWTWSYBXIWSDTLZACMYBWRYCACDEUSUTABCDF
      IVAVBZVCVDWRXIWSWTVECXDHFVFVGXMXEXDXIXRXPPOZABUGZRZXPXDGYFJUHXMXIXPTLZOZY
      GXPPXKWRYIWTXKXIYHXIWSVHDHVIZVJVDABCXPTUIVKVLVMVTVNVOVPVQVRVOVSXAGCNXBCNZ
      XCXHWAABCXPGYJJWBXAWTYBFWCEWDZYKWSWTWEWSYBWTYDWFXACEFWGZYLWSYMWTWSYMABCED
      FIWHWIWFCEFWJVKECHFWMVGAKCGXBAKGYFJYEABKWKWLAKHFKQHLAUCAKFXTIXSABKWKWLWNW
      OWPWQ $.
      $( [17-May-2010] $)
  $}

  ${
    $d G a b m n x y $.  $d G a f m n x y $.  $d M a b m n x y $.
    $d M a f m n x y $.  $d M a b x y z $.
    curgrpact.1 $e |- M e. A $.
    curgrpact.2 $e |- ran M =/= (/) $.
    $( The currying of a group action is a group homomorphism between the group
       ` G ` and the symetry group ` ( SymGrp `` ran M ) ` .  (Contributed by
       FL, 17-May-2010.) $)
    curgrpact $p |- ( <. G , M >. e. GrpAct ->
      ( cur1 ` M ) e. ( G GrpHom ( SymGrp ` ran M ) ) ) $=
      ( vf vx vy va vm wcel cfv co wi wceq wral wa cvv adantr ax-17 vz vb ccur1
      vn cop cga crn csymg cghom cgr cxp wf cgi cv w3a eqid isga2 wf1o wb simp1
      cab elexi rnex symggrpi wfo symgfo forn eqcomd ax-mp sylancl wfn wfun cdm
      elghom c0 wne rnexg 3ad2ant1 a1i ffn 3ad2ant2 jctil domrancur1c ffun 3syl
      jca31 syl21anc fdm df-fn sylanbrc jctir simpr 3jca valcurfn bicomd biimpi
      rngapm syl3anc hbra1 hban hbral hb3an ad2antrr simpllr simplr valvalcurfn
      syl hbra2 jca anim12i syl33anc eqeq12 biimpd gaplc syl113anc syl9r mpd ex
      ralrimi dff1o6 syl3anbrc fvex f1oeq1 elab sylibr ralrimiv copab valcurfn2
      imp 3adant3 adantl simpl gapm2 eqeltrrd impcom ovex oveq2 3ad2ant3 3eqtrd
      eqeq12d ffnfv simprl simprr grpocl 3expib oveq12 ccom adantlr symgov 3exp
      adantll fovrn com12 ancoms rgenw fnopab2g sylib fnopabco2b syl2anc oveq2d
      fvopab4g 2ralbidv anbi12d cbvralv ra4 ra42 com23 sylbi imp31 eqtrd eqeq2d
      id syl6 pm5.32da opabbidv eqcom mpdan expr mpbir2and syl6bi ) CAKZBCUEUFK
      ZCUCLZBCUGZUHLZUIMKZNDUWAUWBBUJKZBUGZUWDUKZUWDCULZBUMLZFUNZCMZUWLOZGUNZHU
      NZBMZUWLCMZUWOUWPUWLCMZCMZOZHUWHPZGUWHPZQZFUWDPZUOZUWFFGHAUWKBCUWHUWDUWHU
      PZUWDUPZUWKUPUQZUXFUWFUWHUWDUWDUWLURZFVAZUWCULZUWOUWCLZUWPUWCLZUWEMZUWQUW
      CLZOZHUWHPZGUWHPZUXFUWGUWEUJKUWFUXLUXSQUSUWGUWJUXEUTUWDCCADVBZVCZVDGHUWCB
      UWEUXKUWHUXGUXKUXKUKZUXKUWEVEZUXKUWEUGZOFUWDUXKUYAUXKUPVFUYCUYDUXKUYBUXKU
      WEVGVHVIVNVJUXFUWCUWHVKZIUNZUWCLZUXKKZIUWHPUXLUXFUWCVLZUWCVMUWHOZUYEUXFUW
      HRKZUWDRKZQZUWDVOVPZCUWIVKZQZQUWHUWDUWDUAUNZULUAVAZUWCULZUYIUXFUYKUYLUYPU
      WGUWJUYKUXEBUJVQVRZUYLUXFUWAUYLDCAVQVIZVSZUXFUYOUYNUWJUWGUYOUXEUWIUWDCVTW
      AZEWBZWFUWHUWDRRUACWCZUWHUYRUWCWDWEUXFUYSUYJUXFUYKUYLUYPUYSUYTVUBVUDVUEWG
      UWHUYRUWCWHXGUWCUWHWIWJUXFUYHIUWHUXFUYFUWHKZUYHUXFVUFQZUWDUWDUYGURZUYHVUG
      UYGUWDVKZUYGUGUWDOZUWOUYGLZUWPUYGLZOZUWOUWPOZNZHUWDPZGUWDPVUHVUGUYMUYPVUF
      UOUWDUWDUYGULVUIVUGUYMUYPVUFUXFUYMVUFUXFUYKUYLUYTVUAWKZSZVUGUYOUYNUXFUYOV
      UFVUCSEWBZUXFVUFWLZWMUWHUWDRRCUYFWNUWDUWDUYGVTWEVUGUWAUWBVUFUYNQVUJUWAVUG
      DVSUXFUWBVUFUXFUWBUWAUXFUWBUSDUWAUWBUXFUXIWOVIWPZSZVUGVUFUYNVUTEWKUYFABCU
      WHUWDUXGUXHWQWRVUGVUPGUWDUXFVUFGUWGUWJUXEGUWGGTUWJGTUXDGFUWDUWLUWDKZGTUWN
      UXCGUWNGTUXBGUWHWSWTXAXBZVUFGTWTVUGUWOUWDKZVUPVUGVVEQZVUOHUWDVUGVVEHUXFVU
      FHUWGUWJUXEHUWGHTUWJHTUXDHFUWDVVCHTUWNUXCHUWNHTUXAGHUWHUWHXHWTXAXBZVUFHTW
      TVVEHTWTVVFUWPUWDKZVUOVVFVVHQZVUKUYFUWOCMZOZVULUYFUWPCMZOZQZVUOVVIUYMUYPV
      UFVVEQZUYMUYPVUFVVHQZVVNVUGUYMVVEVVHVURXCZVUGUYPVVEVVHVUSXCZVVIVUFVVEUXFV
      UFVVEVVHXDZVUGVVEVVHXEZXIVVQVVRVVIVUFVVHVVSVVFVVHWLZXIUYMUYPVVOUOVVKUYMUY
      PVVPUOVVMUWHUWDRRUWOCUYFXFUWHUWDRRUWPCUYFXFXJXKVVNVUMVVJVVLOZVVIVUNVVNVUM
      VWBVUKVVJVULVVLXLXMVVICRKZUWBVUFVVEVVHVWBVUNNVWCVVIUXTVSVUGUWBVVEVVHVVBXC
      VVSVVTVWAUWOUWPUYFBCRUWHUWDUXGUXHXNXOXPXQXRXSXRXSGHUWDUWDUYGXTYAUXJVUHFUY
      GUYFUWCYBUWDUWDUWLUYGYCYDYEXRYFIUWHUXKUWCUUAWJUXFUXRGUWHVVDUXFUWOUWHKZUXR
      UXFVWDQUXQHUWHUXFVWDHVVGVWDHTWTUXFVWDUWPUWHKZUXQUXFVWDVWEQZQZUXMUYFUWDKUB
      UNUWOUYFCMZOQIUBYGZOZUXNJUNZUWDKZUDUNZUWPVWKCMZOQJUDYGZOZUXPVWLVWMUWQVWKC
      MZOZQZJUDYGZOZUOZUXQVWGVWJVWPVXAVWGUYMUYPVWDVWJUXFUYMVWFVUQSZUXFUYPVWFVUD
      SZUXFVWDVWEUUBZIUBUWHUWDRRCUWOYHWRVWGUYMUYPVWEVWPVXCVXDUXFVWDVWEUUCZJUDUW
      HUWDRRCUWPYHWRVWGUYMUYPUWQUWHKZVXAVXCVXDUXFVWFVXGUWGUWJVWFVXGNUXEUWGVWDVW
      EVXGUWOUWPBUWHUXGUUDUUEVRYIJUDUWHUWDRRCUWQYHWRWMVWGVXBQZUXOVWIVWOUWEMZVWT
      UXPVXBUXOVXIOZVWGVWJVWPVXJVXAUXMVWIUXNVWOUWEUUFYJYKVXHVXIVWIVWOUUGZVWLVWM
      VWNVWILZOZQZJUDYGZVWTVXHVWIUWDUWDUYQURZUAVAZKZVWOVXQKZQZVXIVXKOVXBVWGVXTV
      WJVWPVWGVXTNVXAVWJVWPQZVWGVXTVYAVWGQVXRVXSVWJVWGVXRVWPVWJVWGQUXMVWIVXQVWJ
      VWGYLVWGUXMVXQKZVWJVWGUWDUWDUXMURZVYBVWGUWAUWBVWDUYNQVYCUWAVWGDVSZUXFUWBV
      WFVVASZVWGVWDUYNVXEEWKUWOABCUWHUWDUXGUXHYMWRVXPVYCUAUXMUWOUWCYBUWDUWDUYQU
      XMYCYDYEYKYNUUHVWPVWGVXSVWJVWPVWGQUXNVWOVXQVWPVWGYLVWGUXNVXQKZVWPVWGUWDUW
      DUXNURZVYFVWGUWAUWBVWEUYNQVYGVYDVYEVWGVWEUYNVXFEWKUWPABCUWHUWDUXGUXHYMWRV
      XPVYGUAUXNUWPUWCYBUWDUWDUYQUXNYCYDYEYKYNUUKXIXRYJYOUAUWDVXQVWIVWOVUAVXQUP
      UUIXGVXHVWNUWDKZJUWDPZVWIUWDVKZVXKVXOOVWGVYIVXBVWFUXFVYIVWFUXFQVYHJUWDVWF
      UXFVWLVYHNZVWEUXFVYKNVWDUXFVWEVYKUWJUWGVWEVYKNUXEUWJVWEVWLVYHUWPVWKUWDUWH
      UWDCUULUUJWAUUMYKZYIYFUUNSVWGVYJVXBVWGVWHRKZIUWDPZVYJVYNVWGVYMIUWDUWOUYFC
      YPUUOVSIUBUWDVWHVWIVWIUPZUUPUUQSVYIVYJQVXOVXKJUDUWDVWNUWDVWOVXOVWIVWOUPVX
      OUPUURVHUUSVWGVXOVWTOVXBVWGVXNVWSJUDVWGVWLVXMVWRVWGVWLQZVXLVWQVWMVYPVXLUW
      OVWNCMZVWQVYPVYHVYQRKVXLVYQOVWGVWLVYHVWFUXFVYKVYLYOYIUWOVWNCYPIUBVWNVWHVY
      QUWDRVWIUYFVWNUWOCYQVYOUVAVJUXFVWFVWLVYQVWQOZUXEUWGVWFVWLVYRNNZUWJUXEUWKV
      WKCMZVWKOZVWQVYQOZHUWHPGUWHPZQZJUWDPZVYSUXDWUDFJUWDUWLVWKOZUWNWUAUXCWUCWU
      FUWMVYTUWLVWKUWLVWKUWKCYQWUFUVLYTWUFUXAWUBGHUWHUWHWUFUWRVWQUWTVYQUWLVWKUW
      QCYQWUFUWSVWNUWOCUWLVWKUWPCYQUUTYTUVBUVCUVDWUEVWLVWFVYRWUEVWLWUDVWFVYRNZW
      UDJUWDUVEWUCWUGWUAWUCVWFVYRWUCVWFQVWQVYQWUCVWFWUBWUBGHUWHUWHUVFYIVHXRYKUV
      MUVGUVHYRUVIUVJUVKUVNUVOSYSVXBVWTUXPOZVWGVXAVWJWUHVWPVXAWUHUXPVWTUVPWPYRY
      KYSUVQUVRXSXRXSUVSUVTVI $.
      $( [17-May-2010] $)
  $}

  ${
    grpdivfo.1 $e |- X = ran G $.
    grpdivfo.2 $e |- D = ( /g ` G ) $.
    ${
      grpdivfo.3 $e |- U = ( Id ` G ) $.
      $( "Division" by the neutral element of a group.  (Contributed by FL,
         21-Jun-2010.) $)
      grpodivone $p |- ( ( G e. GrpOp /\ A e. X ) -> ( A D U ) = A ) $=
        ( cgr wcel co wceq grpoidcl w3a cgn cfv eqid wi eqtr ex grpoinvid oveq2
        grpodivval wa grporid syl5com 3adant3 com12 syl6 com3l 3ad2ant1 pm2.43i
        3syl mpd 3exp mpid imp ) DIJZAEJZACBKZALZURUSCEJZVACDEFHMURUSVBVAURUSVB
        NZUTACDOPZPZDKZLZVAACBDVDEFVDQZGUCVCVGVARZURUSVCVIRZVBURVECLVFACDKZLZVJ
        CDVDHVHUAVECADUBVGVLVCVAVGVLUTVKLZVCVARVGVLVMUTVFVKSTVCVMVAURUSVMVARVBU
        RUSUDVKALZVMVAACDEFHUEVMVNVAUTVKASTUFUGUHUIUJUMUKULUNUOUPUQ $.
        $( [21-Jun-2010] $)
    $}

    ${
      $d D x y z $.  $d G x y z $.  $d X x y z $.
      $( Todo: maybe suppress grpdivf. $)
      $( A "division" maps onto the group's underlying set.  (Contributed by
         FL, 21-Jun-2010.) $)
      grpodivfo $p |- ( G e. GrpOp -> D : ( X X. X ) -onto-> X ) $=
        ( vx vy vz cgr wcel cxp wf crn wceq cv co wrex wi syl 3exp wfo grpodivf
        cab wfn ffn fnrnov 3syl w3a fovrn com4l rexlimdv rexlimiv com12 cgi cfv
        eleq1a eqid grpoidcl grpodivone rcla4eov adantl com3r eqcoms mpcom mpid
        wa ex impbid abbi1dv eqtrd dffo2 sylanbrc ) BIJZCCKZCALZAMZCNVNCAUAABCD
        EUBZVMVPFOZGOZHOZAPZNZHCQZGCQZFUCZCVMVOAVNUDVPWENVQVNCAUEGHFCCAUFUGVMWD
        FCVMWDVRCJZWDVMWFWCVMWFRZGCVSCJZWBWGHCVMWHVTCJZWBWFVMVOWHWIWBWFRZRRVQVO
        WHWIWJVOWHWIUHWACJWJVSVTCCCAUIWACVRUPSTSUJUKULUMVMWFBUNUOZCJZWDWKBCDWKU
        QZURVMWFWLWDRZVRWKAPZVRNVMWFVFZWNVRAWKBCDEWMUSWPWNRVRWOWPWLVRWONZWDWFWL
        WQWDRRVMWFWLWQWDGHCCVRWKVRAUTTVAVBVCVDVGVEVHVIVJVNCAVKVL $.
        $( [21-Jun-2010] $)
    $}
  $}


  ${
    grpdrcan.1 $e |- X = ran G $.
    grpdrcan.2 $e |- D = ( /g ` G ) $.
    $( Right cancellation law for group "subtraction" (or "division").
       (Contributed by FL, 14-Sep-2010.) $)
    grpodrcan $p |- ( ( G e. GrpOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A D C ) = ( B D C ) <-> A = B ) ) $=
      ( co cgn cfv wceq cgr wcel w3a wb grpodivval bibi1d syl5ibr mpcom wa eqid
      3adant3r2 3adant3r1 simpr1 simpr2 grpoinvcl 3ad2antr3 3jca grporcan eqeq2
      syldan eqeq1 ) ACDIZACEJKZKZEIZLZEMNZAFNZBFNZCFNZOZUAZUNBCDIZLZABLZPZUSUT
      VBURVAACDEUOFGUOUBZHQUCVDVHURUQVELZVGPZVEBUPEIZLZVDVKUSVAVBVMUTBCDEUOFGVI
      HQUDVDVKVMUQVLLZVGPZUSVCUTVAUPFNZOVOVDUTVAVPUSUTVAVBUEUSUTVAVBUFUSUTVBVPV
      ACEUOFGVIUGUHUIABUPEFGUJULVMVJVNVGVEVLUQUKRSTURVFVJVGUNUQVEUMRST $.
      $( [14-Sep-2010] $)


    $( Left cancellation law for group "subtraction" (or "division").
       (Contributed by FL, 14-Sep-2010.) $)
    grpodlcan $p |- ( ( G e. GrpOp /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C D A ) = ( C D B ) <-> A = B ) ) $=
      ( co cfv wceq wa wcel w3a wb wi grpodivval 3exp com13 imp cgn cgr 3adant2
      eqid impcom 3adant1 jca simpl grpoinvcl 3ad2antr1 3ad2antr2 simpr3 adantl
      3jca grpolcan syl wf1 wf1o grpoinvf f1of1 ad2antrl 3simpa ad2antll f1fveq
      syl2anc bitrd ex eqeq12 bibi1d sylibrd mpcom ) CADIZCAEUAJZJZEIZKZCBDIZCB
      VMJZEIZKZLZEUBMZAFMZBFMZCFMZNZLZVLVQKZABKZOZWGVPVTWFWBVPWCWEWBVPPZWDWCWEW
      KWBWEWCVPWBWEWCVPCADEVMFGVMUDZHQRSTUCUEWFWBVTWDWEWBVTPZWCWDWEWMWBWEWDVTWB
      WEWDVTCBDEVMFGWLHQRSTUFUEUGWAWGVOVSKZWIOZWJWAWGWOWAWGLZWNVNVRKZWIWPWBVNFM
      ZVRFMZWENZLZWNWQOWGXAWAWGWBWTWBWFUHWGWRWSWEWBWDWCWRWEAEVMFGWLUIUJWBWCWDWS
      WEBEVMFGWLUIUKWBWCWDWEULUNUGUMVNVRCEFGUOUPWPFFVMUQZWCWDLZWQWIOWBXBWAWFWBF
      FVMURXBEVMFGWLUSFFVMUTUPVAWFXCWAWBWCWDWEVBVCFFABVMVDVEVFVGWAWHWNWIVLVOVQV
      SVHVIVJVK $.
      $( [14-Sep-2010] $)
  $}


  ${
    grpdivzer.1 $e |- X = ran G $.
    grpdivzer.2 $e |- U = ( Id ` G ) $.
    grpdivzer.3 $e |- D = ( /g ` G ) $.
    $( Condition for a "subtraction" (or "division") value to be equal to the
       identity element.  (Contributed by FL, 14-Sep-2010.) $)
    grpodivzer $p |- ( ( G e. GrpOp /\ A e. X /\ B e. X ) ->
                   ( ( A D B ) = U <-> A = B ) ) $=
      ( cgr wcel w3a co wceq cgn cfv eqid grpodivval eqeq1d wb 3adant2 syld3an3
      grpoinvcl grpoinvid1 wf1 wa wf1o grpoinvf f1of1 syl 3simpc f1fveq syl2anc
      3ad2ant1 3bitr2d ) EJKZAFKZBFKZLZABCMZDNABEOPZPZEMZDNZAVAPVBNZABNZUSUTVCD
      ABCEVAFGVAQZIRSUPUQURVBFKZVEVDTUPURVHUQBEVAFGVGUCUAAVBDEVAFGHVGUDUBUSFFVA
      UEZUQURUFVEVFTUPUQVIURUPFFVAUGVIEVAFGVGUHFFVAUIUJUNUPUQURUKFFABVAULUMUO
      $.
      $( [14-Sep-2010] $)
  $}

