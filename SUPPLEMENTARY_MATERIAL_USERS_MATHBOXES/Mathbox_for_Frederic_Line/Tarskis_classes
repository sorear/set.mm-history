$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c tar $. $( A function to study Tarski's classes. $)

  $( Extends class notation to include function ` tar ` . $)
  ctar $a class tar $.

  ${
    $d a b c x y u v $.
    $( A function to study Tarski's classes.  See ~ valdom for its domain,
       ~ vtare for its value at ` (/) ` , ~ vtarsu for its value at a
       successor, ~ vtarl for its value at a limit ordinal. $)
    df-tar $a |- tar = { <. <. a , b >. , c >. | c = ( rec
         ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
           { u | E. v e. x u = ~P v } ) u.
            ~P x ) i^i ( tarskiMap ` a ) ) } , { a } ) |` b ) } $.
  $}

  ${
    $d X a b c x y $.  $d Y a b c $.  $d a b c u v x y $.
    $( The ` tar ` function as a recursive function.  (Contributed by FL,
       20-Mar-2011.) $)
    valtar $p |- ( ( X e. A /\ Y e. B ) -> ( tar ` <. X , Y >. ) = ( rec
       ( { <. x , y >. | y = ( ( ( { u | E. v e. x u C_ v } u.
         { u | E. v e. x u = ~P v } ) u.
          ~P x ) i^i ( tarskiMap ` X ) ) } , { X } ) |` Y ) ) $=
      ( vc va vb wcel wa cfv cv wceq ctskm crdg cres cvv cop ctar wss cab copab
      wrex cpw cun cin csn copab2 co df-tar fveq1i a1i df-ov eqcomi elex adantr
      adantl wfun con0 wfn rdgfnon fnfun resfunexg sylancom fveq2 ineq2d eqeq2d
      opabbidv rdgeq1 sneq rdgeq2 eqtrd reseq1d reseq2 wrel reloprab reldmoprab
      syl cxp cdm resid2 mp2an resoprab eqtr3i ov2g syl3anc 3eqtrd ) GELZHFLZMZ
      GHUAZUBNZWNIOBOZDOZCOZUCCAOZUFDUDWQWRUGPCWSUFDUDUHWSUGUHZJOZQNZUIZPZABUEZ
      XAUJZRZKOZSZPZJKIUKZNZGHXKULZWPWTGQNZUIZPZABUEZGUJZRZHSZWOXLPWMWNUBXKABCD
      JKIUMUNUOXLXMPWMXMXLGHXKUPUQUOWMGTLZHTLZXTTLZXMXTPWKYAWLGEURUSWLYBWKHFURU
      TWKWLXSVAZYCWMXSVBVCZYDYEWMXRXQVDUOVBXSVEWAXSHFVFVGJKIGHTTXIXTXKXSXHSTXAG
      PZXGXSXHYFXGXQXFRZXSYFXEXQPXGYGPYFXDXPABYFXCXOWPYFXBXNWTXAGQVHVIVJVKXFXEX
      QVLWAYFXFXRPYGXSPXAGVMXFXRXQVNWAVOVPXHHXSVQXKTTWBSZXKXATLXHTLMXJMJKIUKXKV
      RXKWCVRYHXKPXJJKIVSXJJKIVTXKWDWEXJJKITTWFWGWHWIWJ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d w x y z X $.
    $( The domain of the ` tar ` function is the ordinal ` Y ` .  (Contributed
       by FL, 20-Mar-2011.) $)
    valdom $p |- ( ( X e. A /\ Y e. On )
      -> dom ( tar ` <. X , Y >. ) = Y ) $=
      ( vy vw vz vx cfv cv wss wrex cab cpw wceq cun cin wcel con0 cdm wi ctskm
      cop ctar copab csn crdg cres wa valtar dmres wfn rdgfnon fndm onss biimpi
      df-ss ineq2 eqeq1d syl5ibr syl5com adantl ax-mp syl5eq dmeq mpcom ) BCUBU
      CHZDIEIZFIZJFGIZKELVGVHMNFVIKELOVIMOBUAHPNGDUDZBUEZUFZCUGZNZBAQZCRQZUHZVF
      SZCNZGDFEARBCUIVQVSVNVMSZCNVQVTCVLSZPZCVLCUJVLRUKZVQWBCNZTVKVJULWCWARNZVQ
      WDRVLUMVPWEWDTVOVPCRJZWEWDCUNWFWDWECRPZCNZWFWHCRUPUOWEWBWGCWARCUQURUSUTVA
      UTVBVCVNVRVTCVFVMVDURUSVE $.
      $( [20-Mar-2011] $)

    $( Value of ` tar ` at ` (/) ` .  JFM CLASSES1 th. 10.  (Contributed by FL,
       20-Mar-2011.) $)
    vtare $p |- ( ( X e. A /\ Y e. On /\ Y =/= (/) )
     -> ( ( tar ` <. X , Y >. ) ` (/) ) = { X } ) $=
      ( vy vw vz vx wcel con0 c0 wne cfv wceq cv wrex cab cpw cun syl cvv ctskm
      w3a cop ctar csn wss cin copab crdg cres on0eln0 biimpar 3adant1 snex a1i
      fvres rdg0g 3ad2ant1 eqtrd wb wa valtar fveq1d eqeq1d 3adant3 mpbird ) BA
      HZCIHZCJKZUBZJBCUCUDLZLZBUEZMZJDNENZFNZUFFGNZOEPVOVPQMFVQOEPRVQQRBUALUGMG
      DUHZVMUIZCUJZLZVMMZVJWAJVSLZVMVJJCHZWAWCMVHVIWDVGVHWDVICUKULUMJCVSUPSVGVH
      WCVMMZVIVGVMTHZWEWFVGBUNUOVMTVRUQSURUSVGVHVNWBUTVIVGVHVAZVLWAVMWGJVKVTGDF
      EAIBCVBVCVDVEVF $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d u v x y A $.  $d u v x y X $.  $d u v x y Y $.
    $( The parts and the powersets of the elements of ` tar ( A ) ` are
       elements of ` tar ( suc A ) ` .  As well as the parts of ` tar ( A ) `
       when they are elements of the smallest Tarski's class of which ` X ` is
       an element.  JFM CLASSES1 th. 11.  (Contributed by FL, 20-Mar-2011.) $)
    vtarsu $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( ( tar `
    <. X , Y >. ) ` suc A ) = ( ( ( { u | E. v e. ( ( tar ` <. X , Y
    >. ) ` A ) u C_ v } u. { u | E. v e. ( ( tar ` <. X , Y >. ) ` A
    ) u = ~P v } ) u. ~P ( ( tar ` <. X , Y >. ) ` A ) ) i^i (
    tarskiMap ` X ) ) ) $=
      ( vy vx wcel con0 cfv cv wrex cab cpw wceq cun ctskm cin cvv csuc w3a cop
      ctar wss copab csn crdg cres valtar 3adant3 fveq1d fvres ordsuccl2 onelon
      3ad2ant3 syldan 3adant1 rdgsuc syl eqcomd fveq2d wa fvex inex2 a1i abbidv
      rexeq uneq12d pweq ineq1d fvopabg sylancr eqtrd 3eqtrd ) EDIZFJIZCUAZFIZU
      BZVREFUCUDKZKVRGLBLZALZUEZAHLZMZBNZWBWCOPZAWEMZBNZQZWEOZQZERKZSZPHGUFZEUG
      ZUHZFUIZKZVRWRKZWDACWAKZMZBNZWHAXBMZBNZQZXBOZQZWNSZVTVRWAWSVPVQWAWSPVSHGA
      BDJEFUJZUKULVSVPWTXAPVQVRFWRUMUPVTXACWRKZWPKZCWSKZWPKZXJVTCJIZXAXMPVQVSXP
      VPVQVSCFIZXPCFUNZFCUOUQURWQCWPUSUTVTXLXNWPVTXQXLXNPVQVSXQVPXRURXQXNXLCFWR
      UMVAUTVBVTXOXBWPKZXJVTXNXBWPVTCWSWAVPVQWSWAPVSVPVQVCWAWSXKVAUKULVBVTXBTIX
      JTIZXSXJPCWAVDXTVTWNXIERVDVEVFHGXBWOXJTTWEXBPZWMXIWNYAWKXGWLXHYAWGXDWJXFY
      AWFXCBWDAWEXBVHVGYAWIXEBWHAWEXBVHVGVIWEXBVJVIVKVLVMVNVOVO $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d u v x y z X $.
    $( The value of ` tar ` at a limit ordinal.  JFM CLASSES1 th. 12.
       (Contributed by FL, 20-Mar-2011.) $)
    vtarl $p |- ( ( X e. B /\ Y e. On /\ ( A e. Y /\ Lim A ) )
      -> ( ( tar ` <. X , Y >. ) ` A ) = U. ( ( tar ` <. X , Y >. ) " A ) ) $=
      ( vy vu vv vx wcel con0 wa cfv cv wrex cab cpw wceq cun cima cuni w3a cop
      wlim ctar wss ctskm cin copab csn crdg valtar fveq1d 3adant3 simp3l fvres
      cres rdglim 3ad2ant3 word 3ad2ant2 imresord syl2anc eqcomd imaeq1d eqtr3d
      syl eloni unieqd eqtrd 3eqtrd ) CBIZDJIZADIZAUCZKZUAZACDUBUDLZLZAEMFMZGMZ
      UEGHMZNFOVSVTPQGWANFORWAPRCUFLUGQHEUHZCUIZUJZDUPZLZAWDLZVQASZTZVKVLVRWFQV
      OVKVLKAVQWEHEGFBJCDUKZULUMVPVMWFWGQVKVLVMVNUNZADWDUOVFVPWGWDASZTZWIVOVKWG
      WMQVLWCADWBUQURVPWLWHVPWEASZWLWHVPDUSZVMWNWLQVLVKWOVODVGUTWKADWDVAVBVPWEV
      QAVPVQWEVKVLVQWEQVOWJUMVCVDVEVHVIVJ $.
      $( [20-Mar-2011] $)
  $}


  ${
    $d A b c $.  $d B b $.  $d X b c u v w x y z $.  $d Y b c u v $.
    $( The sequence ` tar ` has its values in a Tarski's class.  (Contributed
       by FL, 20-Mar-2011.) $)
    tartarmap $p |- ( ( X e. A /\ Y e. On /\ suc B e. Y ) ->
      ( ( tar ` <. X , Y >. ) ` B ) C_ ( tarskiMap ` X ) ) $=
      ( vc csuc con0 wcel w3a cfv wss wi cv c0 wceq sseq1d cpw syl wral wa ctar
      vb vu vv vy vw vz vx cop ctskm onelon 3adant1 sucelon suceq 3anbi3d fveq2
      eleq1d imbi12d csn wne ne0i vtare syl3an3 3ad2ant1 snssd eqsstrd wrex cab
      tskmid cun cin inss2 wb simp1 simp2 word eloni ordtr 3ad2ant2 simp3 trsuc
      wtr syl2anc 3jca 3ad2ant3 vtarsu mpbiri 3exp wlim cima cuni simp31 simp32
      ordsuccl2 adantl simpl 3adant2 vtarl syl3anc r19.27av ordon ordtr1 sylbir
      jca ax-mp ancoms ordsucelsuc simpl1 simpl2 exp3acom23 3imp1 expcom syl6bi
      a1i syl6 pm2.43a pm5.31 ex com3r imp impcom ralimiaa r19.26 3simpa ralimi
      wfun cdm copab crdg cres valtar rdgfnon fnfun funres mp2b funeq ordsuccl3
      wfn 3adant3 valdom sseqtr4d funimass4 fvex elpw ralbii syl6bb uniss unipw
      syl6sseq syl6bir com23 sylbi 3syl pm2.43d 3imp tfinds mpcom ) BFZGHZCAHZD
      GHZUURDHZIZBCDUIUAJZJZCUJJZKZUVAUVBUUSUUTDUURUKULUUSBGHUVCUVGLZBUMUUTUVAU
      BMZFZDHZIZUVIUVDJZUVFKZLUUTUVANFZDHZIZNUVDJZUVFKZLUUTUVAEMZFZDHZIZUVTUVDJ
      ZUVFKZLZUUTUVAUWAFZDHZIZUWAUVDJZUVFKZLUVHUBEBUVINOZUVLUVQUVNUVSUWLUVKUVPU
      UTUVAUWLUVJUVODUVINUNUQUOUWLUVMUVRUVFUVINUVDUPPURUVIUVTOZUVLUWCUVNUWEUWMU
      VKUWBUUTUVAUWMUVJUWADUVIUVTUNUQUOUWMUVMUWDUVFUVIUVTUVDUPPURUVIUWAOZUVLUWI
      UVNUWKUWNUVKUWHUUTUVAUWNUVJUWGDUVIUWAUNUQUOUWNUVMUWJUVFUVIUWAUVDUPPURUVIB
      OZUVLUVCUVNUVGUWOUVKUVBUUTUVAUWOUVJUURDUVIBUNUQUOUWOUVMUVEUVFUVIBUVDUPPUR
      UVQUVRCUSZUVFUVPUUTUVADNUTUVRUWPODUVOVAACDVBVCUVQCUVFUUTUVACUVFHUVPCAVIVD
      VEVFUVTGHZUWFUWIUWKUWQUWFUWIIZUWKUCMZUDMZKUDUWDVGUCVHUWSUWTQOUDUWDVGUCVHV
      JUWDQVJZUVFVKZUVFKZUXAUVFVLUWRUWCUWKUXCVMUWIUWQUWCUWFUWIUUTUVAUWBUUTUVAUW
      HVNUUTUVAUWHVOUWIDWBZUWHUWBUVAUUTUXDUWHUVADVPZUXDDVQZDVRRVSUUTUVAUWHVTDUW
      AWAWCWDWEUWCUWJUXBUVFUDUCUVTACDWFPRWGWHUVIWIZUWFEUVISZUVLUVNUXGUXHUVLIZUV
      MUVDUVIWJZWKZUVFUXIUUTUVAUVIDHZUXGTZUVMUXKOUXGUXHUUTUVAUVKWLUXGUXHUUTUVAU
      VKWMUXGUVLUXMUXHUXGUVLTUXLUXGUVLUXLUXGUVAUVKUXLUUTUVIDWNULWOUXGUVLWPXDWQU
      VIACDWRWSUXGUXHUVLUXKUVFKZUXHUVLUXNLZLUXGUXHUVLUXNUXHUVLUXOUXHUVLTUWFUVLT
      ZEUVISUWEUWCTZEUVISZUXOUWFUVLEUVIWTUXPUXQEUVIUXPUVTUVIHZUXQUWFUVLUXSUXQLU
      VLUXSUWFUXQUVLUXSUWCUWFUXQLUXSUVLUWCUVLUVIVPZUXSUVLUWCLZLUVAUVKUXTUUTUVKU
      VAUXTUVKUVATZUVJGHZUXTGVPUYBUYCLXAUVJDGXBXEUYCUVIGHUXTUVIUMUVIVQXCRXFULUX
      TUXSUWAUVJHZUYAUVTUVIXGUVLUYDUWCUVLUYDTUUTUVAUWBUUTUVAUVKUYDXHUUTUVAUVKUY
      DXIUUTUVAUVKUYDUWBUUTUVAUXEUVKUYDUWBLLUVAUXELUUTUXFXNUXEUYDUVKUWBUWAUVJDX
      BXJXOXKWDXLXMRXPUWFUWCUXQUWCUWFUWCUXQLUWCUWEUWCXQXRXPXOXSXTYAYBUXRUWEEUVI
      SZUWCEUVISZTUXOUWEUWCEUVIYCUYFUYEUXOUYFUUTUVATZEUVISZUYEUXOLUWCUYGEUVIUUT
      UVAUWBYDYEUYHUVLUYEUXNUYHUVLUVDYFZUVIUVDYGZKZTZUYEUXNLUYHUVLUYLUYHUVLTUYI
      UYKUVLUYIUYHUUTUVAUYIUVKUYGUVDUEMUFMZUGMZKUGUHMZVGUFVHUYMUYNQOUGUYOVGUFVH
      VJUYOQVJUVFVKOUHUEYHZUWPYIZDYJZOZUYIUHUEUGUFAGCDYKUYSUYIUYRYFZUYQGYRUYQYF
      UYTUWPUYPYLGUYQYMDUYQYNYOUVDUYRYPWGRYSWOUVLUYKUYHUVLUVIDUYJUVAUVKUVIDKUUT
      UVIDYQULUUTUVAUYJDOUVKACDYTYSUUAWOXDXRUYLUYEUXJUVFQZKZUXNUYLVUBUWDVUAHZEU
      VISUYEEUVIVUAUVDUUBVUCUWEEUVIUWDUVFUVTUVDUUCUUDUUEUUFVUBUXKVUAWKUVFUXJVUA
      UUGUVFUUHUUIUUJXOUUKRYAUULUUMXRUUNXNUUOVFWHUUPXCUUQ $.
      $( [20-Mar-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does ` ~P A ` .  CLASSES1 th. 7.  (Contributed by FL, 17-Apr-2011.)
       (The proof was shortened by Mario Carneiro, 21-Sep-2014.) $)
    pwtsm $p |- ( A e. ( tarskiMap ` X ) -> ~P A e. ( tarskiMap ` X ) ) $=
      ( ctskm cfv ctsk wcel cpw tskmcl tskpw mpan ) BCDZEFAKFAGKFBHAKIJ $.
      $( [21-Sep-2014] $) $( [17-Apr-2011] $)
  $}

  ${
    $d A t $.  $d X t $.
    $( If ` A ` belongs to the smallest Tarski's class that contains ` X ` so
       does the subsets of ` A ` .  CLASSES1. th. 6.  (Contributed by FL,
       17-Apr-2011.)  (The proof was shortened by Mario Carneiro,
       21-Sep-2014.) $)
    subtsm $p |- ( A e. ( tarskiMap ` X ) -> ~P A C_ ( tarskiMap ` X ) ) $=
      ( ctskm cfv ctsk wcel cpw wss tskmcl tskpwss mpan ) BCDZEFALFAGLHBIALJK
      $.
      $( [21-Sep-2014] $) $( [17-Apr-2011] $)
  $}

  $( If ` A ` is a subset of the smallest Tarski's class that contains ` X `
     then it is equipotent to this class or it belongs to it.  CLASSES1 th. 8.
     (Contributed by FL, 17-Apr-2011.) $)
  subtareqbe $p |- ( A C_ ( tarskiMap ` X ) ->
      ( A ~~ ( tarskiMap ` X ) \/ A e. ( tarskiMap ` X ) ) ) $=
    ( ctskm cfv ctsk wcel wss cen wbr wo tskmcl tsken mpan ) BCDZEFANGANHIANFJB
    KANLM $.
    $( [17-Apr-2011] $)

  $c tr $.

  $( $)

  $( Extend class notation to include the function whose value is the
     transitive closure of its operand. $)
  ctr $a class tr $.

  ${
    $d x y a z w $.
    $( The transitive closure of a set. $)
    df-trcls $a |- tr = { <. x , y >. | y = U_ a e. om ( ( rec ( { <. z , w >.
      | w = ( z u. U. z ) } , x ) |` om ) ` a ) } $.
  $}

  ${
    $d A a x y $.  $d a w x y z $.
    $( The transitive closure of a set A. (Contributed by FL, 17-Apr-2011.) $)
    trclval $p |- ( A e. B
    -> ( tr ` A ) = U_ a e. om
     ( ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) ` a ) ) $=
      ( vx vy wcel cvv com cv cuni cun wceq copab crdg cres cfv ciun ctr rdgeq2
      elex omex fvex iunex reseq1d fveq1d adantr iuneq2dv df-trcls wrel relopab
      wa resid ax-mp resopab 3eqtr2i fvopab4g sylancl ) CDHCIHEJEKZBKAKZVALMNAB
      OZCPZJQZRZSZIHCTRVFNCDUBEJVEUCUTVDUDUEFGCEJUTVBFKZPZJQZRZSZVFIITVGCNZEJVJ
      VEVLVJVENUTJHVLUTVIVDVLVHVCJVGCVBUAUFUGUHUITGKVKNZFGOZVNIQZVGIHVMUMFGOFGA
      BEUJVNUKVOVNNVMFGULVNUNUOVMFGIUPUQURUS $.
      $( [17-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d A v z $.  $d B v $.  $d C u v $.  $d C v z $.  $d X u v $.
    $d X v z $.  $d Y u v $.  $d Y v z $.
    $( C belongs to the value of ` tar ` at a successor of ` A ` iff it is a
       part of ` tar ` at ` A ` , the powerset of an element or a part of an
       element of ` tar ` at ` A ` .  CLASSES1 th. 13 (Contributed by FL,
       13-Apr-2011.)  (The proof was shortened by Mario Carneiro,
       21-Sep-2014.) $)
    vtarsuelt $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) -> ( C e. ( ( tar
    ` <. X , Y >. ) ` suc A ) <-> ( ( C C_ ( ( tar ` <. X , Y >. ) ` A ) /\
    C e. ( tarskiMap ` X ) ) \/ E. z e. ( ( tar ` <. X , Y >. ) ` A )
    ( C C_ z \/ C = ~P z ) ) ) ) $=
      ( vu vv wcel cfv cv wss wrex cpw wceq wa wo wb a1i anbi1d w3a cop cab cun
      con0 csuc ctar ctskm cin vtarsu eleq2d elin elun andir orbi1d orcom elpwg
      pm5.32rd sseq1 rexbidv elabg eqeq1 orbi12d r19.43 bicomi tartarmap sselda
      wi ctsk tskmcl tskss mp3an1 pwtsm eleq1 syl5ibrcom jaod syl pm4.71 bicomd
      ex sylib rexbidva r19.41v sseq2 pweq eqeq2d cbvrexv 3bitr3g 3bitrd syl5bb
      bitrd ) ECIFUEIBUFZFIUAZDWLEFUBUGJZJZIDGKZHKZLZHBWNJZMZGUCZWPWQNZOZHWSMZG
      UCZUDZWSNZUDZEUHJZUIZIZDWSLZDXIIZPZDAKZLZDXONZOZQZAWSMZQZWMWOXJDHGBCEFUJU
      KWMXKDXHIZXMPZDXFIZDXGIZQZXMPZYAXKYCRWMDXHXIULSWMYBYFXMYBYFRWMDXFXGUMSTWM
      YGYDXMPZYEXMPZQZDXAIZDXEIZQZXMPZYIQZYAYGYJRWMYDYEXMUNSWMYHYNYIWMYDYMXMYDY
      MRWMDXAXEUMSTUOYOYIYNQWMYAYNYIUPWMYIXNYNXTWMXMYEXLXMYEXLRVHWMDWSXIUQSURWM
      YNDWQLZHWSMZDXBOZHWSMZQZXMPYPYRQZHWSMZXMPZXTWMXMYMYTXMYMYTRVHWMXMYKYQYLYS
      WTYQGDXIWPDOZWRYPHWSWPDWQUSUTVAXDYSGDXIUUDXCYRHWSWPDXBVBUTVAVCSURWMYTUUBX
      MYTUUBRWMUUBYTYPYRHWSVDVESTWMUUAXMPZHWSMUUBUUCXTWMUUEUUAHWSWMWQWSIPZUUAUU
      EUUFUUAXMVHZUUAUUERUUFWQXIIZUUGWMWSXIWQCBEFVFVGUUHYPXMYRUUHYPXMXIVIIUUHYP
      XMEVJWQDXIVKVLVTUUHXMYRXBXIIWQEVMDXBXIVNVOVPVQUUAXMVRWAVSWBUUAXMHWSWCUUAX
      SHAWSWQXOOZYPXPYRXRWQXODWDUUIXBXQDWQXOWEWFVCWGWHWIVCWJWIWIWK $.
      $( [21-Sep-2014] $) $( [13-Apr-2011] $)
  $}

  ${
    $d A z $.  $d C z $.  $d X z $.  $d Y z $.  $d Z z $.
    $( If ` C ` is a part of an element of our tar function at ` A ` then ` C `
       is an element or tar at ` suc A ` .  CLASSES1 th. 14 (Contributed by FL,
       13-Apr-2011.) $)
    partarelt1 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( C C_ Z /\ Z e. ( ( tar ` <. X , Y >. ) ` A ) ) ->
          C e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vz wss cop ctar cfv wcel wa con0 csuc w3a cpw wceq wo wi orc wrex sseq2
      cv pweq eqeq2d orbi12d rcla4ev ctskm olc vtarsuelt syl5ibrcom mpan9 com12
      syl ex ) CFHZFADEIJKZKZLZMDBLENLAOZELPZCVAURKLZUQUQCFQZRZSZUTVBVCTZUQVEUA
      UTVFVGUTVFMCGUDZHZCVHQZRZSZGUSUBZVGVLVFGFUSVHFRZVIUQVKVEVHFCUCVNVJVDCVHFU
      EUFUGUHVMVCVBCUSHCDUIKLMZVMSVMVOUJGABCDEUKULUOUPUMUN $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A u v $.  $d X u v $.  $d Y u v $.  $d Z u v $.
    $( If ` Z ` is an element of our tar function at ` A ` then ` ~P Z ` is an
       element or tar at ` suc A ` .  CLASSES1 th. 15 (Contributed by FL,
       13-Apr-2011.) $)
    partarelt2 $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( Z e. ( ( tar ` <. X , Y >. ) ` A ) ->
          ~P Z e. ( ( tar ` <. X , Y >. ) ` suc A ) ) ) $=
      ( vu vv wcel con0 csuc w3a cfv cpw cv wrex cab wceq cun cvv syl cop ctskm
      ctar wa wss cin simpr eqidd eqeq2d rcla4ev syl2anc wb pwexg eqeq1 rexbidv
      pweq elabg adantl mpbird elun2 elun1 3syl tartarmap sselda pwtsm sylanbrc
      elin vtarsu adantr eleqtrrd ex ) CBHDIHAJZDHKZEACDUAUCLZLZHZEMZVLVNLZHVMV
      PUDZVQFNZGNZUEGVOOFPZVTWAMZQZGVOOZFPZRZVOMZRZCUBLZUFZVRVSVQWIHZVQWJHZVQWK
      HVSVQWFHZVQWGHWLVSWNVQWCQZGVOOZVSVPVQVQQZWPVMVPUGVSVQUHWOWQGEVOWAEQWCVQVQ
      WAEUPUIUJUKVPWNWPULZVMVPVQSHWREVOUMWEWPFVQSVTVQQWDWOGVOVTVQWCUNUOUQTURUSV
      QWFWBUTVQWGWHVAVBVSEWJHWMVMVOWJEBACDVCVDECVETVQWIWJVGVFVMVRWKQVPGFABCDVHV
      IVJVK $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d X x $.  $d Y x $.
    $( All the element of ` tar ` at ` A ` are elements of ` tar ` at
       ` suc A ` .  CLASSES1 th. 18 (Contributed by FL, 13-Apr-2011.) $)
    tareltsuc $p |- ( ( X e. B /\ Y e. On /\ suc A e. Y ) ->
       ( ( tar ` <. X , Y >. ) ` A ) C_
         ( ( tar ` <. X , Y >. ) ` suc A ) ) $=
      ( vx wcel con0 csuc w3a cop ctar cfv cv wss ssid partarelt1 mpani ssrdv )
      CBFDGFAHZDFIZEACDJKLZLZSUALZTEMZUDNUDUBFUDUCFUDOABUDCDUDPQR $.
      $( [13-Apr-2011] $)
  $}

  $( An element of the intersection of a Tarski's class with the class of the
     ordinal numbers is a part of the intersection.  (Contributed by FL,
     20-Apr-2011.) $)
  eltintpar $p |- ( T e. Tarski -> ( A e. ( On i^i T )
      -> A C_ ( On i^i T ) ) ) $=
    ( ctsk wcel con0 cin wss wa elin onss ad2antrr word wtr eloni ordtr tsktrss
    wi 3exp com3l ex 3syl imp31 jca sylbi impcom ssin sylib ) BCDZAEBFZDZAUIGZU
    HUJHAEGZABGZHZUKUJUHUNUJAEDZABDZHZUHUNQAEBIUQUHUNUQUHHULUMUOULUPUHAJKUOUPUH
    UMUOALAMZUPUHUMQQANAOUHURUPUMUHURUPUMABPRSUAUBUCTUDUEAEBUFUGT $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The intersection of a Tarski's class with the class of the ordinal
       numbers is an ordinal number.  (Contributed by FL, 20-Apr-2011.) $)
    inttaror $p |- ( T e. Tarski -> ( On i^i T ) e. On ) $=
      ( vx ctsk wcel con0 cin cvv cv wa wral incom inex1g syl5eqelr inss1 sseli
      wss adantl wi elin word wtr eloni onss w3a tsktrss ssin biimpi ex syl5com
      ordtr 3exp com14 syl com12 3syl pm2.43i imp sylbi impcom ralrimiva celsor
      jca syl2anc ) ACDZEAFZGDBHZEDZVFVEPZIZBVEJVEEDVDVEAEFGAEKAECLMVDVIBVEVDVF
      VEDZIVGVHVJVGVDVEEVFEANOQVJVDVHVJVGVFADZIVDVHRZVFEASVGVKVLVGVKVLRZVGVFTVF
      UAZVGVMRVFUBVFUJVGVNVMVGVFEPZVNVMRVFUCVDVNVKVOVHVDVNVKVOVHRVDVNVKUDVFAPZV
      OVHVFAUEVOVPVHVOVPIVHVFEAUFUGUHUIUKULUMUNUOUPUQURUSVBUTBVEGVAVC $.
      $( [20-Apr-2011] $)
  $}

  $( The intersection of a Tarski's class and the ordinal numbers is equipotent
     to the Tarski's class.  JFM CLASSES2. .  (Contributed by FL,
     20-Apr-2011.) $)
  inttarcar $p |- ( T e. Tarski -> ( On i^i T ) ~~ T ) $=
    ( ctsk wcel con0 cin wss cen wbr inss2 wo wa tsken ax-1 inttaror elin elirr
    wi pm2.21i sylbir ex syl adantr com12 jaoi mpcom mpan2 ) ABCZDAEZAFZUHAGHZD
    AIUJUHACZJUGUIKZUJUHALUJULUJQUKUJULMULUKUJUGUKUJQZUIUGUHDCZUMANUNUKUJUNUKKU
    HUHCZUJUHDAOUOUJUHPRSTUAUBUCUDUEUF $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of the intersection of a Tarski's class with the class of
       the ordinal numbers.  (Contributed by FL, 20-Apr-2011.) $)
    carinttar $p |- ( T e. Tarski
      -> ( card ` ( On i^i T ) ) = ( On i^i T ) ) $=
      ( vx ctsk wcel con0 cin cv csdm wbr wral ccrd wa cen wn cvv imp wi expcom
      mpcom sylanbrc cfv wceq inttaror cdom wss incom inex1g syl5eqel eltintpar
      ssdom2g sylsyld inttarcar elin tsksdom sdomnen con3d a1i com3r syl adantl
      entr sylbi com13 brsdom ralrimiva iscard ) ACDZEAFZEDBGZVHHIZBVHJVHKUAVHU
      BAUCVGVJBVHVGVIVHDZLVIVHUDIZVIVHMIZNZVJVGVKVLVGVHODVKVIVHUEVLVGVHAEFOEAUF
      AECUGUHVIAUIVIVHOUJUKPVGVKVNVHAMIZVGVKVNQAULVKVGVOVNVKVIEDZVIADZLVGVOVNQZ
      QZVIEAUMVQVSVPVGVQVRVIAHIZVGVQLZVRVIAUNVTVIAMIZNZWAVRQVIAUOWAVOWCVNVOWCVN
      QQWAVOVMWBVMVOWBVIVHAVARUPUQURUSSRUTVBVCSPVIVHVDTVEBVHVFT $.
      $( [20-Apr-2011] $)
  $}

  $( The cardinal of a Tarski's class equals the intersection of the Tarski's
     class with the class of the ordinal numbers.  CLASSES2 th. 10.
     (Contributed by FL, 20-Apr-2011.) $)
  carinttar2 $p |- ( T e. Tarski
      -> ( card ` T ) = ( On i^i T ) ) $=
    ( ctsk wcel ccrd cfv con0 cin wceq cen wbr inttarcar ensymg mpd wb inttaror
    carden mpdan mpbird carinttar eqtrd ) ABCZADEZFAGZDEZUCUAUBUDHZAUCIJZUAUCAI
    JUFAKUCABLMUAUCFCUEUFNAOAUCBFPQRAST $.
    $( [20-Apr-2011] $)

  ${
    $d T x $.
    $( The cardinal of an element of a Tarski's class belongs to the Tarski's
       class. th. 12 CLASSES2 (Contributed by FL, 20-Apr-2011.) $)
    cardtar $p |- ( ( T e. Tarski /\ A e. T ) -> ( card ` A ) e. T ) $=
      ( ctsk wcel wa ccrd cfv wss con0 cin carinttar2 inss2 eqsstrd adantr csdm
      a1i wbr tsksdom wb cardsdom ancoms mpbird sseldd ) BCDZABDZEZBFGZBAFGZUDU
      GBHUEUDUGIBJZBBKUIBHUDIBLPMNUFUHUGDZABOQZABRUEUDUJUKSABBCTUAUBUC $.
      $( [20-Apr-2011] $)

    $( The cardinal of a Tarski's class is a limit ordinal.  CLASSES2 th. 21.
       (Contributed by FL, 20-Apr-2011.)  (The proof was shortened by Mario
       Carneiro, 21-Sep-2014.) $)
    cartarlim $p |- ( ( T e. Tarski /\ T =/= (/) ) -> Lim ( card ` T ) ) $=
      ( ctsk wcel c0 wne wa com ccrd cfv wss wlim cardom cdom wbr tskinf cvv wb
      omex simpl carddom sylancr mpbird syl5eqssr cardlim sylib ) ABCZADEZFZGAH
      IZJUIKUHGGHIZUILUHUJUIJZGAMNZAOUHGPCUFUKULQRUFUGSGAPBTUAUBUCAUDUE $.
      $( [21-Sep-2014] $) $( [20-Apr-2011] $)
  $}

  ${
    $( An element of the cardinal of the Tarski's class ` T ` is an element of
       ` T ` . th. 14 CLASSES2.  (Contributed by FL, 20-Nov-2011.) $)
    elcarelcl $p |- ( ( T e. Tarski /\ A e. ( card ` T ) )
    -> A e. T ) $=
      ( ctsk wcel ccrd cfv con0 cin wceq wi carinttar2 eleq2 simprbi syl6bi syl
      elin imp ) BCDZABEFZDZABDZRSGBHZIZTUAJBKUCTAUBDZUASUBALUDAGDUAAGBPMNOQ $.
      $( [20-Nov-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element of a transitive
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23.  (Contributed by FL, 26-Sep-2011.) $)
    fnctartar $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> ( T ^m A ) C_ T ) $=
      ( vf vx ctsk wcel w3a cv wi wa wss csdm wbr cfv wceq com12 3adant2 imbi2d
      imp cen wtr cmap co elmapg simpr1 wel cxp fssxp ssel c1st c2nd cop simp31
      wf elxp6 trel exp3acom23 3adant1 adantr simp1r tskop syl3anc 3exp syl5ibr
      eleq1 sylbi syl6com com4l mpcom ssrdv wfn wfun cdm ffn fnfun fndm jca vex
      fundmen breq1 tsksdom cvv sdomen1 biimpd mpan syl5com syl6bi 3syl 3jca ex
      mpan9 tskssel syl6 pm2.43a ) BEFZBUAZABFZGZCBAUBUCZBCHZWSFZWRWTBFZWOWQXAW
      RXBIZIWPWOWQJZXAABWTUNZXCBAWTEBUDXEWRWOWTBKZWTBLMZGZXBXEWRXHXEWRJZWOXFXGX
      EWOWPWQUEXIDWTBXEWRDCUFZDHZBFZIZWTABUGZKZXEWRXMIABWTUHXJXOXEWRXLXOXJXKXNF
      ZXEWRXLIZIZWTXNXKUIXPXKXKUJNZXKUKNZULZOZXSAFZXTBFZJZJXRXKABUOYBYEXRYEXRYB
      XEWRYABFZIZIYEXEWRYFYEXEWRGWOXSBFZYDYFYEXEWOWPWQUMYEWRYHXEYEWRYHYCWRYHIYD
      WRYCYHWPWQYCYHIZWOWPWQYIWPYCWQYHBXSAUPUQSURPUSSQYCYDXEWRUTXSXTBVAVBVCYBXQ
      YGXEYBXLYFWRXKYABVERRVDSVFVGVHVISVJXEWRXGXEWTAVKZWTVLZWTVMZAOZJWRXGIZABWT
      VNYJYKYMAWTVOAWTVPVQYKYLWTTMZYMYNWTCVRZVSYMYOAWTTMZYNYLAWTTVTWRYQXGWOWQYQ
      XGIWPXDABLMZYQXGABWAWTWBFZYQYRXGIYPYSYQJYRXGAWTBWBWCWDWEWFQPWGWKWHSWIWJWT
      BWLWMWGQWNVJ $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d A f x $.  $d T f x $.
    $( Consider functions whose domain ` A ` is an element and a part of a
       Tarski's class ` T ` and whose range is ` T ` , then they are elements
       of ` T ` .  CLASSES2 th. 23.  (Contributed by FL, 27-Sep-2011.) $)
    fnctartar2 $p |- ( ( T e. Tarski /\ A C_ T /\ A e. T ) -> ( T ^m A ) C_ T )
        $=
      ( vf vx ctsk wcel wss w3a cv csdm wbr wi 3adant2 wa ssel cfv wceq imp cen
      com12 cmap co wf elmapg simpr1 wel cxp fssxp c1st c2nd cop elxp6 3ad2ant2
      simp31 adantr simp1r tskop syl3anc 3exp eleq1 syl5ibr sylbi syl6com com4l
      wb imbi2d mpcom ssrdv wfn wfun cdm ffn jca fnfun fndm anim12i vex fundmen
      breq1 tsksdom cvv sdomen1 biimpd mpan syl5com syl6bi 3syl 3jca ex pm2.43a
      mpan9 tskssel syl6 ) BEFZABGZABFZHZCBAUAUBZBWQCIZWRFZWNWSBGZWSBJKZHZWSBFW
      TWQXCWQWTABWSUCZWQXCLWNWPWTXDVEWOBAWSEBUDMXDWQXCXDWQNZWNXAXBXDWNWOWPUEXED
      WSBXDWQDCUFZDIZBFZLZWSABUGZGZXDWQXILABWSUHXFXKXDWQXHXKXFXGXJFZXDWQXHLZLZW
      SXJXGOXLXGXGUIPZXGUJPZUKZQZXOAFZXPBFZNZNXNXGABULXRYAXNYAXNXRXDWQXQBFZLZLY
      AXDWQYBYAXDWQHWNXOBFZXTYBYAXDWNWOWPUNYAWQYDXDYAWQYDXSWQYDLXTWQXSYDWOWNXSY
      DLWPABXOOUMTUORMXSXTXDWQUPXOXPBUQURUSXRXMYCXDXRXHYBWQXGXQBUTVFVFVARVBVCVD
      VGRVHXDWQXBXDWSAVIZYENWSVJZWSVKZAQZNWQXBLZXDYEYEABWSVLZYJVMYEYFYEYHAWSVNA
      WSVOVPYFYGWSSKZYHYIWSCVQZVRYHYKAWSSKZYIYGAWSSVSWQYMXBWNWPYMXBLWOWNWPNABJK
      ZYMXBABVTWSWAFZYMYNXBLYLYOYMNYNXBAWSBWAWBWCWDWEMTWFWKWGRWHWIWFWJWSBWLWMVH
      $.
      $( [27-Sep-2011] $)
  $}

  $( If the cardinal of ` A ` of a part of ` T ` is less than ` T ` . a
     function from ` A ` to ` T ` is a part of ` T ` .  CLASSES2 th. 23.
     (Contributed by FL, 20-Nov-2011.) $)
  fnctartar3 $p |- ( ( T e. Tarski /\ A C_ T /\
      ( card ` A ) e. ( card ` T ) ) -> ( T ^m A ) C_ T ) $=
    ( ctsk wcel wss ccrd cfv cmap co csdm wbr wa wb ssexg ancoms simpl cardsdom
    cvv syl2anc syld3an3 biimp3a tskssel fnctartar2 ) BCDZABEZAFGBFGDZABDZBAHIB
    EUDUEUFABJKZUGUDUEUFUHUDUELARDZUDUFUHMUEUDUIABCNOUDUEPABRCQSUAABUBTABUCT $.
    $( [20-Nov-2011] $)

