$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              The "maps to" notation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d A y $.  $d B y $.  $d x y $.
    cmp.1 $e |- F = ( x e. A |-> B ) $.
    $( Functionality of a class given by a "maps to" notation.  (Contributed by
       FL, 17-Feb-2008.) $)
    cmpfun $p |- Fun F $=
      ( wfun cmpt funmpt funeqi mpbir ) DFABCGZFABCHDKEIJ $.
      $( [31-May-2014] $) $( [17-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.
    cmpdom.1 $e |- F = ( x e. A |-> B ) $.
    $( Domain of a class given by the "maps to" notation.  (Contributed by FL,
       17-Feb-2008.) $)
    cmpdom $p |- ( A. x e. A B e. _V <-> dom F = A ) $=
      ( cvv wcel wral cdm wceq wfun wfn wa df-fn mptfng 3bitr4i cmpfun mpbiran2
      ancom ) CFGABHZDIBJZDKZDBLUBUAMTUAUBMDBNABCDEOUAUBSPABCDEQR $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x $.
    cmpdom2.1 $e |- F = ( x e. A |-> ( B G C ) ) $.
    $( Domain of a class given by the "maps to" notation.  (Contributed by FL,
       21-Jun-2010.) $)
    cmpdom2 $p |- dom F = A $=
      ( co cvv wcel wral cdm wceq ovex rgenw cmpdom mpbi ) CDFHZIJZABKELBMSABCD
      FNOABREGPQ $.
      $( [21-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C y z $.  $d F x z $.
    $( Functionality of an ordered-pair class abstraction given by the "maps
       to" notation.  (Contributed by FL, 17-May-2010.)  (The proof was
       shortened by Mario Carneiro, 31-May-2014.) $)
    fopab2g $p |- ( F = { <. x , y >. | ( x e. A /\ y = C ) } ->
       ( A. x e. A C e. B <-> F : A --> B ) ) $=
      ( cv wcel wceq wa copab wf wral feq1 eqid fopab2 syl6rbbr ) FAGCHBGEIJABK
      ZICDFLCDRLEDHACMCDFRNABCDERROPQ $.
      $( [31-May-2014] $) $( [17-May-2010] $)
  $}

  ${
    $d A x $.
    $( Composition of a restricted identity and a mapping (using the maps to
       notation).  See ~ fcoi1 .  (Contributed by FL, 25-Apr-2012.) $)
    crimmt1 $p |- ( F : A --> B -> ( F o. ( x e. A |-> x ) ) = F ) $=
      ( wf cv cmpt ccom cid cres mptresid coeq2i fcoi1 syl5eq ) BCDEDABAFGZHDIB
      JZHDOPDABKLBCDMN $.
      $( [25-Apr-2012] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Composition of a restricted identity and a mapping (using the maps to
       notation).  See ~ fcoi2 .  (Contributed by FL, 25-Apr-2012.) $)
    crimmt2 $p |- ( F : A --> B -> ( ( x e. B |-> x ) o. F ) = F ) $=
      ( wf cv cmpt ccom cid cres mptresid coeq1i fcoi2 syl5eq ) BCDEACAFGZDHICJ
      ZDHDOPDACKLBCDMN $.
      $( [25-Apr-2012] $)
  $}

  ${
    $d A f x $.  $d A1 f x $.  $d B f x $.  $d B1 f x $.  $d E f x $.
    $d F1 x $.  $d G f x $.
    injsurinj.1 $e |- F1 = ( f e. ( B ^m A ) |-> ( ( E o. f ) o. G ) ) $.
    $( Function returning a composite.  (Contributed by FL, 19-Nov-2011.) $)
    mapmapmap $p |- ( ( E : B --> B1 /\ G : A1 --> A /\
      ( ( A e. _V /\ B e. _V ) /\ ( A1 e. _V /\ B1 e. _V ) ) )
      -> F1 : ( B ^m A ) --> ( B1 ^m A1 ) ) $=
      ( vx wf cvv wcel wa wral ccom wi coexg jca adantr w3a cmap co wfn cfv fex
      cv expcom ad2antlr impcom 3adant2 vex sylancl ad2antrl a1i 3imp ralrimivw
      com12 ralimi syl mptfng sylib simpr syl2anc coeq2 coeq1d fvmptg simpl1 wb
      wceq pm3.22 3ad2ant3 elmapg biimpa fco simpl2 syl6 sylibrd pm2.43i adantl
      feq1 3syl mpbird ralrimiva ffnfv sylanbrc ) BGDKZFAEKZALMZBLMZNZFLMZGLMZN
      ZNZUAZHBAUBUCZUDZJUGZHUEZGFUBUCZMZJWQOWQXAHKWPDCUGZPZEPZLMZCWQOZWRWPXDLMZ
      ELMZNZCWQOXGWPXJCWQWPXHXIWPDLMZXCLMXHWGWOXKWHWOWGXKWJWGXKQWIWNWGWJXKBGLDU
      FUHUIUJUKZCULDXCLLRUMWGWHWOXIWHWOXIQQWGWOWHXIWLWHXIQWKWMWHWLXIFALEUFUHUNU
      RUOUPZSUQXJXFCWQXDELLRUSUTCWQXEHIVAVBWPXBJWQWPWSWQMZNZXBFGWTKZXOXPXOXNDWS
      PZEPZLMZNWTXRVJZXOXPQXOXNXSWPXNVCXOXQLMZXIXSXOXKWSLMYAWPXKXNXLTJULDWSLLRU
      MWPXIXNXMTXQELLRVDSCWSXEXRWQLHXCWSVJXDXQEXCWSDVEVFIVGXTXOFGXRKZXPXTXOAGXQ
      KZWHNZYBXOYDQXTXOYCWHXOWGABWSKZYCWGWHWOXNVHWPXNYEWPWJWINZXNYEVIWOWGYFWHWK
      YFWNWIWJVKTVLBAWSLLVMUTVNABGDWSVOVDWGWHWOXNVPSUOFAGXQEVOVQFGWTXRWAVRWBVSX
      OWMWLNZXBXPVIWPYGXNWOWGYGWHWNYGWKWLWMVKVTVLTGFWTLLVMUTWCWDJWQXAHWEWF $.
      $( [19-Nov-2011] $)
  $}

  ${
    $d A f x y $.  $d A1 f x y $.  $d B f x y $.  $d B1 f x y $.  $d E f x y $.
    $d F1 x y $.  $d G f x y $.
    injsuinj.1 $e |- F1 = ( f e. ( B ^m A ) |-> ( ( E o. f ) o. G ) ) $.
    $( If ` E ` is an injection and ` G ` a surjection
       ` ( f |-> ( ( E o. f ) o. G ) ) ` is an injection.  Bourbaki E.II.31
       prop. 2.  (Contributed by FL, 20-Nov-2011.) $)
    injsurinj $p |- ( ( E : B -1-1-> B1 /\ G : A1 -onto-> A
       /\ ( ( A e. _V /\ B e. _V ) /\ ( A1 e. _V /\ B1 e. _V ) ) )
        -> F1 : ( B ^m A ) -1-1-> ( B1 ^m A1 ) ) $=
      ( vx vy cvv wcel wa wf wceq wi ccom syl2anc 3ad2ant3 wf1 wfo w3a cmap cfv
      co cv weq wral f1f fof id mapmapmap syl3an adantr simprlr fex 3adant2 vex
      simp1 coexg sylancl 3ad2ant2 simp3rl jca coeq2 coeq1d fvmptg eqtr simpr31
      simp2 elmapg ancoms biimpd impcom adantl 3adant1 3jca simp32 3ad2ant1 fco
      wb wfn ffn surrc2 syl3anc njtlc sylc syl eqcoms com4t 3syl pm2.43i 3expia
      ex com12 ralrimivv dff13 sylanbrc ) BGDUAZFAEUBZALMZBLMZNZFLMZGLMZNZNZUCZ
      BAUDUFZGFUDUFZHOZJUGZHUEZKUGZHUEZPZJKUHZQZKXJUIJXJUIXJXKHUAWTBGDOZXAFAEOZ
      XHXHXLBGDUJZFAEUKZXHULABCDEFGHIUMUNXIXSJKXJXJXMXJMZXOXJMZNXIXSYDYEXIXSYDY
      EXIUCZXSYFYDDXMRZERZLMZNXNYHPZYFXSQYFYDYIYDYEXIUTXIYDYIYEXIYGLMZELMZYIXID
      LMZXMLMYKWTXHYMXAWTXHNXTXCYMWTXTXHYBUOWTXBXCXGUPBGLDUQSURZJUSDXMLLVAVBXIY
      AXEYLXAWTYAXHYCVCXEXFXDWTXAVDFALEUQSZYGELLVASTVECXMDCUGZRZERZYHXJLHCJUHYQ
      YGEYPXMDVFVGIVHYFYJXSYFYJXSQZYFYEDXORZERZLMZNXPUUAPZYFYSQYFYEUUBYDYEXIVKY
      FYTLMZYLUUBYFYMXOLMUUDXIYDYMYEYNTKUSDXOLLVAVBXIYDYLYEYOTYTELLVASVECXOYRUU
      AXJLHCKUHYQYTEYPXODVFVGIVHYJXQUUCYFXRXQUUCYFXRQZQZQYHXNYHXNPZXQUUFUUGXQNY
      HXPPZUUFYHXNXPVIUUHUUCUUEUUHUUCNYHUUAPZUUEYHXPUUAVIUUIYFXRUUIYFNZWTABXMOZ
      ABXOOZUCYGYTPZXRUUJWTUUKUULWTXAXHYDYEUUIVJYFUUKUUIYDXIUUKYEXIYDUUKXHWTYDU
      UKQZXAXDUUNXGXDYDUUKXCXBYDUUKWBBAXMLLVLVMVNUOTVOURZVPYFUULUUIYEXIUULYDXIY
      EUULXHWTYEUULQZXAXDUUPXGXDYEUULXCXBYEUULWBBAXOLLVLVMVNUOTVOVQZVPVRYFUUIUU
      MYFXAAGYGOZAGYTOZUUIUUMQYDYEWTXAXHVSYFXTUUKUURXIYDXTYEWTXAXTXHYBVTTZUUOAB
      GDXMWASYFXTUULUUSUUTUUQABGDXOWASXAUURUUSUCUUIUUMXAXAUURYGAWCUUSYTAWCUUIUU
      MWBXAULAGYGWDAGYTWDFAEYGYTWEUNVNWFVOABGDXMXOWGWHWOWIWOWIWOWJWKWLWMWPWLWMW
      NWPWQJKXJXKHWRWS $.
      $( [20-Nov-2011] $)
  $}


