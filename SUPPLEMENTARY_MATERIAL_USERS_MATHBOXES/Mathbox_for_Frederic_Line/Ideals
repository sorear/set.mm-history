$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Ideals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c IdlNEW $.

  $( Extend class notation with the class of ideals. $)
  cidln $a class IdlNEW $.

  ${
    $d r i x y z $.
    $( Define the class of (two-sided) ideals of a ring ` R ` .  A subset of
       ` R ` is an ideal if it contains ` 0 ` , is closed under addition, and
       is closed under multiplication on either side by any element of ` R ` .
       (Contributed by Jeff Madsen, 10-Jun-2010.  Modified by FL,
       29-Oct-2014.) $)
    df-idlNEW $a |- IdlNEW = ( r e. Ring |->
      { i e. ~P ( Base ` r ) |
       ( ( 0g ` r ) e. i
       /\ A. x e. i ( A. y e. i ( x ( +g ` r ) y ) e. i
       /\ A. z e. ( Base ` r ) ( ( z ( .r ` r ) x ) e. i /\
                              ( x ( .r ` r ) z ) e. i ) ) ) } ) $.
  $}

  ${
    $d i r x y z R $.  $d i r B $.  $d r P $.  $d r T $.  $d r Z $.
    idlvalNEW.1 $e |- P = ( +g ` R ) $.
    idlvalNEW.2 $e |- T = ( .r ` R ) $.
    idlvalNEW.3 $e |- B = ( Base ` R ) $.
    idlvalNEW.4 $e |- Z = ( 0g ` R ) $.
    $( The class of ideals of a ring.  (Contributed by Jeff Madsen,
       10-Jun-2010.  Modified by FL, 29-Oct-2014.) $)
    idlvalNEW $p |- ( R e. Ring -> ( IdlNEW ` R ) = { i e. ~P B | ( Z e. i /\
      A. x e. i ( A. y e. i ( x P y ) e. i /\
          A. z e. B ( ( z T x ) e. i /\ ( x T z ) e. i ) ) ) } ) $=
      ( cv cfv wcel co wral wa wceq vr c0g cplusg cmulr cbs cpw crg cidln fveq2
      crab eqcomi syl6eq pweqd eleq1d wb oveq adantr ralbidva wi eleq2d anbi12d
      syl imbi12d ralbidv2 rabeqbidv df-idlNEW fvex eqeltri pwex rabex fvmpt
      cvv ) UAFUANZUBOZHNZPZANZBNZVMUCOZQZVOPZBVORZCNZVQVMUDOZQZVOPZVQWCWDQZVOP
      ZSZCVMUEOZRZSZAVORZSZHWJUFZUJIVOPZVQVREQZVOPZBVORZWCVQGQZVOPZVQWCGQZVOPZS
      ZCDRZSZAVORZSZHDUFZUJUGUHVMFTZWNXHHWOXIXJWJDXJWJFUEOZDVMFUEUIDXKLUKULZUMX
      JVPWPWMXGXJVNIVOXJVNFUBOZIVMFUBUIIXMMUKULUNXJWLXFAVOXJVQVOPZSZWBWSWKXEXJW
      BWSUOXNXJWAWRBVOXJWAWRUOVRVOPXJVTWQVOXJVSETVTWQTXJVSFUCOZEVMFUCUIEXPJUKUL
      VQVRVSEUPVBUNUQURUQXOWIXDCWJDXJWCWJPZWIUSWCDPZXDUSUOXNXJXQXRWIXDXJWJDWCXL
      UTXJWFXAWHXCXJWEWTVOXJWDGTZWEWTTXJWDFUDOZGVMFUDUIGXTKUKULZWCVQWDGUPVBUNXJ
      WGXBVOXJXSWGXBTYAVQWCWDGUPVBUNVAVCUQVDVAURVAVEABCHUAVFXHHXIDDXKVLLFUEVGVH
      VIVJVK $.
      $( [17-Nov-2014] $)

    $d i r x y z R $.  $d i r B $.  $d i x y z I $.  $d i r P $.  $d i r T $.
    $d i r Z $.
    $( The predicate "is an ideal of the ring ` R ` ."  (Contributed by Jeff
       Madsen, 10-Jun-2010.  Modified by FL, 29-Oct-2014.) $)
    isidlNEW $p |- ( R e. Ring -> ( I e. ( IdlNEW ` R ) <->
          ( I C_ B /\ Z e. I /\ A. x e. I ( A. y e. I ( x P y ) e. I /\
             A. z e. B ( ( z T x ) e. I /\ ( x T z ) e. I ) ) ) ) ) $=
      ( vi wcel cv co wral wa eleq2 crg cidln cfv cpw crab wss idlvalNEW eleq2d
      w3a cbs fvex eqeltri elpw2 anbi1i raleqbi1dv anbi12d ralbidv elrab 3anass
      cvv wceq 3bitr4i syl6bb ) FUAOZHFUBUCZOHINPZOZAPZBPEQZVFOZBVFRZCPZVHGQZVF
      OZVHVLGQZVFOZSZCDRZSZAVFRZSZNDUDZUEZOZHDUFZIHOZVIHOZBHRZVMHOZVOHOZSZCDRZS
      ZAHRZUIZVDVEWCHABCDEFGNIJKLMUGUHHWBOZWFWNSZSWEWQSWDWOWPWEWQHDDFUJUCUTLFUJ
      UKULUMUNWAWQNHWBVFHVAZVGWFVTWNVFHITVSWMAVFHWRVKWHVRWLVJWGBVFHVFHVITUOWRVQ
      WKCDWRVNWIVPWJVFHVMTVFHVOTUPUQUPUOUPURWEWFWNUSVBVC $.
      $( [17-Nov-2014] $)

  $}


