$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Uniform spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c Unif $. $( The class of all uniform structures. $)

  $( Extend class notation with the uniform structure. $)
  cunif $a class Unif $.

  $( Define the uniform structure component of a uniform space. $)
  df-unif $a |- Unif = ( f e. _V |-> ( f ` 13 ) ) $.

  $c UnifSp $. $( The class of all uniform spaces. $)

  $( Extend class notation with the class of all uniform spaces. $)
  cunifsp $a class UnifSp $.

  ${
    $d f y x t v u w s a z $.
    $( Definition of a uniform space.  Bourbaki TG II.1 def. 1.  A uniform
       structure is used to give a generalization of the idea of Cauchy's
       sequence.  We consider the space is equipped with the topology induced
       by the uniform structure.  (Contributed by FL, 29-May-2014.) $)
    df-unifsp $a |- UnifSp = { f | E. y E. x E. t
     ( ( y = ( Base ` f ) /\ x = ( Unif ` f ) /\ t = ( TopOpen ` f ) )
     /\ ( x C_ ~P ( y X. y )
           /\ ( y X. y ) e. x
           /\ A. v e. x (
               A. u e. ~P ( y X. y ) ( v C_ u -> u e. x )
               /\ A. w e. x ( v i^i w ) e. x
               /\ ( ( _I |` y ) C_ v
                    /\ `' v e. x
                    /\ E. w e. x ( w o. w ) C_ v ) ) )
      /\ t = ( iota_ s e. Top ( U. s = y /\ A. a e. y
             ( ( nei ` s ) ` { a } ) =
                   { z | E. u e. x z = ( u " { a } ) } ) ) ) } $.
  $}


