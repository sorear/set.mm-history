$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Category and deductive system underlying "structure"
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Alg $. $( The class of all the "structures" used by ` Ded `
               and ` Cat ` . $)

  $c dom_ $. $( Function returning the function domain of a category. Despite
  its name the domain of a category is not the same concept as the domain of
  a relation. $)

  $c cod_ $. $( Function returning the function codomain of a category. $)

  $c id_ $. $( Function returning the function identity of a category. $)

  $c o_ $. $( Function returning the composition of morphisms of a category. $)

  $( Extend class notation with the class of structures used by ` Cat ` and
     ` Ded ` . $)
  calg $a class Alg $.

  $( Extend class notation with the function returning the function domain of a
     category. $)
  cdom_ $a class dom_ $.

  $( Extend class notation with the function returning the function codomain of
     a category. $)
  ccod_ $a class cod_ $.

  $( Extend class notation with the function returning the function identity of
     a category. $)
  cid_ $a class id_ $.

  $( Extend class notation with the function returning the composition of
     morphisms of a category. $)
  co_ $a class o_ $.

  ${
    $d x d c j r $.
    $( ` Ded ` and ` Cat ` structure.  Metamath for internal reasons doesn't
       like too large definitions.  Then ` Cat ` has been split giving birth to
       ` Ded ` and ` Alg ` .  If ` Ded ` has a real mathematical use, ` Alg `
       is only here to give relief to Metamath. $)
    df-alg $a |- Alg = { x | E. d E. c E. j E. r
      ( x = <. <. d , c >. , <. j , r >. >. /\
      ( d : dom d --> dom j /\ c : dom d --> dom j /\ j : dom j --> dom d ) /\
      ( Fun r /\ dom r C_ ( dom d X. dom d ) /\ ran r C_ dom d ) ) } $.
  $}

  $( Definition of ` dom_ ` . $)
  df-doma $a |- dom_ = ( 1st o. 1st ) $.

  $( Definition of ` cod_ ` . $)
  df-coda $a |- cod_ = ( 2nd o. 1st ) $.

  $( Definition of ` id_ ` . $)
  df-ida $a |- id_ = ( 1st o. 2nd ) $.

  $( Definition of ` o_ ` . $)
  df-cmpa $a |- o_ = ( 2nd o. 2nd ) $.

  ${
    $d C c d j r $.  $d D c d j r $.  $d J c d j r $.  $d M c d j r $.
    $d O c d j r $.  $d R c d j r $.  $d c d j r x $.
    isalg.1 $e |- M = dom D $.
    isalg.2 $e |- O = dom J $.
    $( The predicate "has the structure required by ` Ded ` and ` Cat ` ."
       (Contributed by FL, 24-Oct-2007.) $)
    isalg $p |- ( ( ( D e. A /\ C e. B /\ J e. F ) /\ R e. G )
                -> ( <. <. D , C >. , <. J , R >. >. e. Alg
                  <-> ( ( D : M --> O /\ C : M --> O /\ J : O --> M )
                    /\ ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) ) ) $=
      ( wcel wceq wf w3a wex wa wb 3syl vx vd vc vj vr cop calg cv cdm wfun cxp
      wss crn df-alg eleq2i 3anass exbii 3exbii abbii feq1 eqcomi eqeq2i biimpi
      cab dmeq feq2 bitrd feq3 3anbi123d syl6eqr biimpri sseq2 3anbi23d anbi12d
      xpid11 3anbi2d anbi1d funeq sseq1d rneq anbi2d elo syl5bb ) DCUFHEUFUFZUG
      MWDUAUHUBUHZUCUHZUFUDUHZUEUHZUFUFNZWEUIZWGUIZWEOZWJWKWFOZWKWJWGOZPZWHUJZW
      HUIZWJWJUKZULZWHUMZWJULZPZPZUEQZUDQUCQUBQZUAVDZMZDAMCBMHFMPEGMRZIJDOZIJCO
      ZJIHOZPZEUJZEUIZIIUKZULZEUMZIULZPZRZUGXFWDUAUDUEUCUBUNUOXGWDWIWOXBRZRZUEQ
      ZUDQUCQUBQZUAVDZMXHXTXFYEWDXEYDUAXDYCUBUCUDXCYBUEWIWOXBUPUQURUSUOYAIWKDOZ
      IWKWFOZWKIWGOZPZWPWQXOULZWTIULZPZRYFIWKCOZYHPZYLRXLYLRXTUAUBUCUEUDDCHEABF
      GWEDNZWOYIXBYLYOWLYFWMYGWNYHYOWLWJWKDOZYFWJWKWEDUTYOWJDUIZNZWJINZYPYFSWED
      VEZYRYSYQIWJIYQKVAVBVCZWJIWKDVFTVGYOYRYSWMYGSYTUUAWJIWKWFVFTYOYRYSWNYHSYT
      UUAWJIWKWGVHTVIYOWSYJXAYKWPYOYSWRXONZWSYJSYOWJYQIYTKVJUUBYSWJIVOVKWRXOWQV
      LTYOYRYSXAYKSYTUUAWJIWTVLTVMVNWFCNZYIYNYLUUCYGYMYFYHIWKWFCUTVPVQWGHNZYNXL
      YLUUDYFXIYMXJYHXKUUDWKHUIZNZWKJNZYFXISWGHVEZUUFUUGUUEJWKJUUELVAVBVCZWKJID
      VHTUUDUUFUUGYMXJSUUHUUIWKJICVHTUUDYHWKIHOZXKWKIWGHUTUUDUUFUUGUUJXKSUUHUUI
      WKJIHVFTVGVIVQWHENZYLXSXLUUKWPXMYJXPYKXRWHEVRUUKWQXNXOWHEVEVSUUKWTXQIWHEV
      TVSVIWAWBWCWC $.
      $( [24-Oct-2007] $)
  $}

  ${
    1alg.1 $e |- A e. _V $.
    $( Category ` 1 ` has the structure required by ` Ded ` and ` Cat ` .
       (Contributed by FL, 30-Oct-2007.) $)
    1alg $p |- <. <. { <. <. A , A >. , A >. } , { <. <. A , A >. , A >. } >. ,
      <. { <. A , <. A , A >. >. } , { <. <. <. A , A >. , <. A , A >. >. ,
      <. A , A >. >. } >. >. e. Alg $=
      ( cop csn calg wcel w3a cdm wss cvv snex 3pm3.2i dmsnop eqcomi wf1o f1osn
      wf opex f1of ax-mp wfun cxp crn wa isalg mp2an funsn fssxp eqsstri rnsnop
      wb eqimssi mpbir2an ) AACZACZDZUPCAUNCZDZUNUNCZUNCZDZCCEFZUNDZADZUPQZVEVD
      VCURQZGZVAUAZVAHZVCVCUBZIZVAUCZVCIZGZUPJFZVOURJFZGVAJFVBVGVNUDUKVOVOVPUOK
      ZVQUQKLUTKJJUPUPVAJJURVCVDUPHVCUNAMNURHVDAUNMNUEUFVEVEVFVCVDUPOVEUNAAARZB
      PVCVDUPSTZVSVDVCUROVFAUNBVRPVDVCURSTLVHVKVMUSUNUNUNRZUGVIUSDZVJUSUNMVCVCW
      AQZWAVJIVCVCWAOWBUNUNVRVRPVCVCWASTVCVCWAUHTUIVLVCUSUNVTVRUJULLUM $.
      $( [30-Oct-2007] $)
  $}

  ${
    domval.1 $e |- D = ( dom_ ` T ) $.
    $( Value of the domain function expressed with the ` 1st ` function.
       (Contributed by FL, 24-Oct-2007.) $)
    domval $p |- D = ( 1st ` ( 1st ` T ) ) $=
      ( cdom_ cfv c1st cvv wcel wceq ccom df-doma fveq1i wfun fo1st fofun ax-mp
      wf wfo c0 fvprc fvco3 mp3an12 syl5eq wn fveq2d 1st0 syl6req eqtrd pm2.61i
      fof eqtri ) ABDEZBFEZFEZCBGHZULUNIUOULBFFJZEZUNBDUPKLFMZGGFQZUOUQUNIGGFRZ
      URNGGFOPUTUSNGGFUJPGGBFFUAUBUCUOUDZULSUNBDTVAUNSFESVAUMSFBFTUEUFUGUHUIUK
      $.
      $( [24-Oct-2007] $)
  $}

  ${
    codval.1 $e |- C = ( cod_ ` T ) $.
    $( Value of the function codomain expressed with the ` 1st ` and ` 2nd `
       functions.  (Contributed by FL, 26-Oct-2007.) $)
    codval $p |- C = ( 2nd ` ( 1st ` T ) ) $=
      ( ccod_ cfv c1st c2nd cvv wcel wceq ccom df-coda fveq1i wfun wf wfo fo2nd
      ax-mp c0 fvprc fofun fo1st fof fvco3 mp3an12 syl5eq wn 2nd0 syl6req eqtrd
      fveq2d pm2.61i eqtri ) ABDEZBFEZGEZCBHIZUNUPJUQUNBGFKZEZUPBDURLMGNZHHFOZU
      QUSUPJHHGPUTQHHGUARHHFPVAUBHHFUCRHHBGFUDUEUFUQUGZUNSUPBDTVBUPSGESVBUOSGBF
      TUKUHUIUJULUM $.
      $( [26-Oct-2007] $)
  $}

  ${
    idval.1 $e |- J = ( id_ ` T ) $.
    $( Value of the identity function expressed with the ` 1st ` and ` 2nd `
       functions.  (Contributed by FL, 26-Oct-2007.) $)
    idval $p |- J = ( 1st ` ( 2nd ` T ) ) $=
      ( cid_ cfv c2nd c1st cvv wcel wceq ccom df-ida fveq1i wfun wf fo1st ax-mp
      wfo c0 fvprc fofun fo2nd fof fvco3 mp3an12 syl5eq wn fveq2d syl6req eqtrd
      1st0 pm2.61i eqtri ) BADEZAFEZGEZCAHIZUNUPJUQUNAGFKZEZUPADURLMGNZHHFOZUQU
      SUPJHHGRUTPHHGUAQHHFRVAUBHHFUCQHHAGFUDUEUFUQUGZUNSUPADTVBUPSGESVBUOSGAFTU
      HUKUIUJULUM $.
      $( [26-Oct-2007] $)
  $}

  ${
    cmpval.1 $e |- G = ( o_ ` T ) $.
    $( Value of the identity function expressed with the ` 2nd ` functions.
       (Contributed by FL, 26-Oct-2007.) $)
    cmpval $p |- G = ( 2nd ` ( 2nd ` T ) ) $=
      ( co_ cfv c2nd cvv wcel wceq ccom df-cmpa fveq1i wf wfo fo2nd fofun ax-mp
      wfun c0 fvprc fof fvco3 mp3an12 syl5eq wn fveq2d 2nd0 syl6req eqtrd eqtri
      pm2.61i ) BADEZAFEZFEZCAGHZULUNIUOULAFFJZEZUNADUPKLFRZGGFMZUOUQUNIGGFNZUR
      OGGFPQUTUSOGGFUAQGGAFFUBUCUDUOUEZULSUNADTVAUNSFESVAUMSFAFTUFUGUHUIUKUJ $.
      $( [26-Oct-2007] $)
  $}

  ${
    $d C c d j r $.  $d D c d j r $.  $d J c d j r $.  $d M c d j r $.
    $d O c d j r $.  $d R c d j r $.  $d T c d j r x $.
    algi.1 $e |- D = ( dom_ ` T ) $.
    algi.2 $e |- C = ( cod_ ` T ) $.
    algi.3 $e |- J = ( id_ ` T ) $.
    algi.4 $e |- R = ( o_ ` T ) $.
    algi.5 $e |- M = dom D $.
    algi.6 $e |- O = dom J $.
    $( "Axiomatic" properties of ` Alg ` .  (Contributed by FL,
       24-Oct-2007.) $)
    algi $p |- ( T e. Alg
         -> ( ( D : M --> O /\ C : M --> O /\ J : O --> M )
               /\ ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) ) $=
      ( vx vd wf w3a wceq wex wb vc vj vr wfun cdm cxp wss crn wa cop calg wcel
      cab 3anass bicomi exbii 3exbii abbii eleq2i c1st cfv domval eqcomi eqeq2i
      feq1 dmeq feq2d feq2i syl6bbr bitrd feq2 syl biimpi feq3 3anbi123d xpid11
      3syl sseq2 sylbir 3anbi23d anbi12d sylbi c2nd codval 3anbi2d anbi1d idval
      cv cmpval funeq sseq1d rneq anbi2d eloi df-alg eleq2s ) FGBPZFGAPZGFEPZQZ
      CUDZCUEZFFUFZUGZCUHZFUGZQZUIZDNWHOWHZUAWHZUJUBWHZUCWHZUJUJRZXIUEZXKUEZXIP
      ZXNXOXJPZXOXNXKPZQZXLUDZXLUEZXNXNUFZUGZXLUHZXNUGZQZQZUCSZUBSUASOSZNUMZUKD
      YJULDXMXSYFUIZUIZUCSZUBSUASOSZNUMZULXHYOYJDYNYINYMYHOUAUBYLYGUCYGYLXMXSYF
      UNUOUPUQURUSYKFXOBPZFXOXJPZXOFXKPZQZXTYAXCUGZYDFUGZQZUIZYPFXOAPZYRQZUUBUI
      ZWTUUBUIZXHNOUAUCUBDXIDUTVAZUTVAZRXIBRZYKUUCTUUIBXIBUUIBDHVBVCVDUUJXSYSYF
      UUBUUJXPYPXQYQXRYRUUJXPXNXOBPZYPXNXOXIBVEUUJUUKBUEZXOBPYPUUJXNUULXOBXIBVF
      ZVGFUULXOBLVHVIVJUUJXNUULRZXQYQTUUMUUNXQUULXOXJPYQXNUULXOXJVKFUULXOXJLVHV
      IVLUUJUUNXNFRZXRYRTUUMUUNUUOUULFXNFUULLVCVDVMZXNFXOXKVNVQVOUUJUUNUUOYFUUB
      TUUMUUPUUOYCYTYEUUAXTUUOYBXCRYCYTTXNFVPYBXCYAVRVSXNFYDVRVTVQWAWBXJUUHWCVA
      ZRXJARZUUCUUFTUUQAXJAUUQADIWDVCVDUURYSUUEUUBUURYQUUDYPYRFXOXJAVEWEWFWBXKD
      WCVAZUTVAZRXKERZUUFUUGTUUTEXKEUUTDEJWGVCVDUVAUUEWTUUBUVAYPWQUUDWRYRWSUVAX
      OEUEZRZXOGRZYPWQTXKEVFZUVCUVDUVBGXOGUVBMVCVDVMZXOGFBVNVQUVAUVCUVDUUDWRTUV
      EUVFXOGFAVNVQUVAYRXOFEPZWSXOFXKEVEUVAUVCUVDUVGWSTUVEUVFXOGFEVKVQVJVOWFWBX
      LUUSWCVAZRXLCRZUUGXHTUVHCXLCUVHDCKWIVCVDUVIUUBXGWTUVIXTXAYTXDUUAXFXLCWJUV
      IYAXBXCXLCVFWKUVIYDXEFXLCWLWKVOWMWBWNVSNUBUCUAOWOWP $.
      $( [24-Oct-2007] $)
  $}

  ${

    alg.1 $e |- M = dom D $.
    alg.3 $e |- D = ( dom_ ` T ) $.
    ${
      alg.2 $e |- O = dom J $.
      alg.5 $e |- J = ( id_ ` T ) $.

      ${
        $( ` ( dom_ `` T ) ` is a mapping from the morphisms of ` T ` to the
           objects of ` T ` .  (Contributed by FL, 24-Oct-2007.) $)
        doma $p |- ( T e. Alg -> D : M --> O ) $=
          ( calg wcel wf ccod_ cfv w3a co_ wfun cdm wss eqid algi simpld simp1d
          cxp crn ) BJKZDEALZDEBMNZLZEDCLZUFUGUIUJOBPNZQUKRDDUDSUKUEDSOUHAUKBCD
          EGUHTIUKTFHUAUBUC $.
          $( [24-Oct-2007] $)
      $}

      ${
        alg.4 $e |- C = ( cod_ ` T ) $.
        $( ` ( cod_ `` T ) ` is a mapping from the morphisms of ` T ` to the
           objects of ` T ` .  (Contributed by FL, 26-Oct-2007.) $)
        coda $p |- ( T e. Alg -> C : M --> O ) $=
          ( calg wcel wf w3a co_ cfv wfun cdm wss cxp eqid algi simpld simp2d
          crn ) CLMZEFBNZEFANZFEDNZUGUHUIUJOCPQZRUKSEEUATUKUFETOABUKCDEFHKJUKUB
          GIUCUDUE $.
          $( [26-Oct-2007] $)

      $}

      ${
        $( ` ( id_ `` A ) ` is a mapping from the objects of ` T ` to the
           morphisms of ` T ` .  (Contributed by FL, 26-Oct-2007.) $)
        ida $p |- ( T e. Alg -> J : O --> M ) $=
          ( calg wcel wf ccod_ cfv w3a co_ wfun cdm wss eqid algi simpld simp3d
          cxp crn ) BJKZDEALZDEBMNZLZEDCLZUFUGUIUJOBPNZQUKRDDUDSUKUEDSOUHAUKBCD
          EGUHTIUKTFHUAUBUC $.
          $( [26-Oct-2007] $)

        $( An identity is a morphism.  (Contributed by FL, 10-Jan-2008.) $)
        idmoa $p |- ( ( T e. Alg /\ A e. O ) -> ( J ` A ) e. M ) $=
          ( calg wcel wf cfv ida ffvelrn sylan ) CKLFEDMAFLADNELBCDEFGHIJOFEADP
          Q $.
          $( [10-Jan-2008] $)
      $}
    $}

    ${
      alg.6 $e |- R = ( o_ ` T ) $.
      $( ` ( o_ `` T ) ` is a partial operation on the morphisms of ` T ` .
         (Contributed by FL, 26-Oct-2007.) $)
      cmppfa $p |- ( T e. Alg ->
        ( Fun R /\ dom R C_ ( M X. M ) /\ ran R C_ M ) ) $=
        ( calg wcel cid_ cfv cdm wf ccod_ w3a wfun cxp wss crn eqid algi simprd
        ) CHIDCJKZLZAMDUDCNKZMUDDUCMOBPBLDDQRBSDROUEABCUCDUDFUETUCTGEUDTUAUB $.
        $( [26-Oct-2007] $)
    $}
  $}

  ${
    dcsda.1 $e |- D = ( dom_ ` T ) $.
    dcsda.2 $e |- C = ( cod_ ` T ) $.
    $( ` ( dom_ `` T ) ` and ` ( cod_ `` T ) ` have the same domain.
       (Contributed by FL, 10-Jan-2008.) $)
    dcsda $p |- ( T e. Alg -> dom D = dom C ) $=
      ( calg wcel cdm cid_ cfv wf wceq eqid coda fdm eqcomd syl ) CFGBHZCIJZHZA
      KZRAHZLABCSRTRMDTMSMENUAUBRRTAOPQ $.
      $( [10-Jan-2008] $)
  $}

