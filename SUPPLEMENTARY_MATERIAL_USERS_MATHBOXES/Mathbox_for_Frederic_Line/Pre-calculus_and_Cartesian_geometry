$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Pre-calculus and Cartesian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real.  (Contributed by FL, 27-Dec-2007.) $)
  dmse1 $p |- ( ( A e. RR /\ B e. RR /\ A =/= B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel wne w3a cmin co cabs cfv crp c2 cdiv cc cc0 wa resubcl recnd wceq
    recn 3adant3 wn subeq0 syl2an biimpd necon3ad 3impia sylibr absrpcl syl2anc
    wb df-ne 2re 2pos elrpii rpdivcl sylancl ) ACDZBCDZABEZFZABGHZIJZKDZLKDVCLM
    HKDVAVBNDZVBOEZVDURUSVEUTURUSPZVBABQRUAVAVBOSZUBZVFURUSUTVIVGVHABVGVHABSZUR
    ANDBNDVHVJUKUSATBTABUCUDUEUFUGVBOULUHVBUIUJLUMUNUOVCLUPUQ $.
    $( [27-Dec-2007] $)

  $( Distance between the middle of a segment and one of its extremities is a
     positive real.  (Contributed by FL, 27-Dec-2007.) $)
  dmse2 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( abs ` ( A - B ) ) / 2 ) e. RR+ ) $=
    ( cr wcel clt wbr wne cmin co cabs cfv c2 cdiv crp w3a ltne necomd syld3an3
    dmse1 ) ACDZBCDZABEFZABGABHIJKLMINDTUAUBOBAABPQABSR $.
    $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real.  (Contributed by
     FL, 27-Dec-2007.) $)
  msr3 $p |- ( ( A e. RR /\ B e. RR ) ->
   ( B - ( ( abs ` ( A - B ) ) / 2 ) ) e. RR ) $=
    ( cr wcel wa cmin co cabs cfv c2 cdiv simpr cc0 wne resubcl recnd abscl syl
    cc a1i 2re 2ne0 redivcl syl3anc syl2anc ) ACDZBCDZEZUGABFGZHIZJKGZCDZBUKFGC
    DUFUGLUHUJCDZJCDZJMNZULUHUISDUMUHUIABOPUIQRUNUHUATUOUHUBTUJJUCUDBUKOUE $.
    $( [27-Dec-2007] $)

  $( The midpoint of a segment AB of the real line is a real.  (To FL:  The
     proof was shortened.  Also, it is too specialized, and set.mm size will be
     reduced if it is placed directly in the proof using it. --NM) (Contributed
     by FL, 27-Dec-2007.) $)
  msr4 $p |- ( ( A e. RR /\ B e. RR ) ->
    ( A + ( ( abs ` ( B - A ) ) / 2 ) ) e. RR ) $=
    ( cr wcel cmin co cabs cfv c2 cdiv caddc wa cc resubcl recnd abscl rehalfcl
    ancoms 3syl readdcl syldan ) ACDZBCDZBAEFZGHZIJFZCDZAUFKFCDUBUCLUDMDZUECDUG
    UCUBUHUCUBLUDBANORUDPUEQSAUFTUA $.
    $( [27-Dec-2007] $)


  $( The midpoint of a segment AB of the real line is on the "left" of ` B ` .
     (Contributed by FL, 2-Jan-2008.) $)
  mslb1 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
     ( A + ( ( abs ` ( B - A ) ) / 2 ) ) < B ) $=
    ( cr wcel clt wbr c2 co cmin caddc wceq cc wa 3syl wb mpbird syl2anc adantr
    cc0 syl w3a cmul cdiv cfv 2cn a1i 3ad2ant1 crp 3simpa anim12i ancoms abssub
    cabs recn dmse2 eqeltrd rpre adddi syl3anc wne subcl abscl 2ne0 divcan2 cle
    oveq1d resubcl 3adant3 ltle 3impia pm3.22 subge0 absid eqtrd oveq2d remulcl
    2re mpan recnd msr4 pm3.2i divmul mp3an3 simp3 simp1 2times simpl3 3ad2ant2
    readdcl pncan breqtrrd simp2 3brtr4d eqbrtrd mpdan negsub sub32 negid subid
    jca cneg eqcomd anidms adantl negcl subadd 3eqtr2rd mulcl 3jca addass addcl
    eqtr4d simpr 3eqtr3d breq1d biimpa ltsub1 2pos ltdivmul eqbrtrrd ) ACDZBCDZ
    ABEFZUAZGAUBHZBAIHZJHZGUCHZAYFUMUDZGUCHZJHZBEYDYHYKKZGYKUBHZYGKZYDYMYEGYJUB
    HZJHZYGYDGLDZALDZYJLDZYMYPKYQYDUEUFZYAYBYRYCAUNZUGZYDYJUHDYJCDYSYDYJABIHUMU
    DZGUCHUHYDYIUUCGUCYDYAYBMZBLDZYRMZYIUUCKYAYBYCUIZYBYAUUFYBUUEYAYRBUNZUUAUJU
    KZBAULNVFABUOUPYJUQYJUNNGAYJURUSYDYOYFYEJYDYOYIYFYDYILDZYQGSUTZYOYIKYDYFLDZ
    YICDUUJYDUUDUUFUULUUGUUIBAVAZNYFVBYIUNNYTUUKYDVCUFYIGVDUSYDYFCDZSYFVEFZYIYF
    KYAYBUUNYCYBYAUUNBAVGUKZVHZYDUUOABVEFZYAYBYCUURABVIVJYDUUDYBYAMUUOUUROUUGYA
    YBVKBAVLNPYFVMQVNVOVNYAYBYLYNOZYCUUDYGLDZYKLDZUUSUUDYGUUDYECDZUUNYGCDZYAUVB
    YBGCDZYAUVBVQGAVPVRZRUUPYEYFWIZQVSUUDYKABVTVSUUTUVAYQUUKMUUSYQUUKUEVCWAYGYK
    GWBWCQVHPYDYHBEFZYGGBUBHZEFZYDYEAIHZUVHBIHZEFZUVIYDYCUVLYAYBYCWDYDYCMZUVJAA
    JHZAIHZUVKEUVMYEUVNAIYDYEUVNKZYCYDYAYRUVPYAYBYCWEZUUAAWFNRVFUVMABBJHZBIHZUV
    OUVKEUVMABUVSEYAYBYCYCWGUVMUUEUUEMZUVSBKYDUVTYCYBYAUVTYCYBUUEUUEUUHUUHWTWHR
    BBWJTWKUVMYRYRMZUVOAKYDUWAYCYAYBUWAYCYAYRYRUUAUUAWTUGRAAWJTUVMUVHUVRBIYDUVH
    UVRKZYCYDYBUUEUWBYAYBYCWLZUUHBWFNRVFWMWNWOYDUVLMZUVIYGBIHZUVKEFZYDUVLUWFYDU
    VJUWEUVKEYDYEAXAZJHZYGBXAZJHZUVJUWEYDUWHYEYFUWIJHZJHZUWJYDUWGUWKYEJYDUWKYFB
    IHZBBIHZAIHZUWGYDUULUUEUWKUWMKYDUUDYRUUEMZUULUUGYAYRYBUUEUUAUUHUJZUUEYRUULU
    UMUKZNYBYAUUEYCUUHWHZYFBWPQYDUUEUUEYRUWOUWMKUWSUWSUUBBBAWQUSYDUWOUWGKZAUWGJ
    HZUWNKZYDUXASUWNYDYAYRUXASKUVQUUAAWRNYBYASUWNKYCYBUWNSYBUUEUWNSKUUHBWSTXBWH
    VNYDUWNLDZYRUWGLDZUWTUXBOYDUUDUWPUXCUUGUWQUUEUXCYRUUEUXCBBVAXCXDNUUBYDYAYRU
    XDUVQUUAAXENUWNAUWGXFUSPXGVOYDYELDZUULUWILDZUAZUWJUWLKYDUUDUWPUXGUUGUWQUWPU
    XEUULUXFYRUXEUUEYQYRUXEUEGAXHVRZRZUWRUUEUXFYRBXEXDXINYEYFUWIXJTXLYDUXEYRUWH
    UVJKYDYAYRUXEUVQUUAUXHNUUBYEAWPQYDUUTUUEMZUWJUWEKYDUUDUWPUXJUUGUWQUWPUUTUUE
    UWPUXEUULUUTUXIUWRYEYFXKQYRUUEXMWTNYGBWPTXNXOXPUWDUVCUVHCDZYBUAZUVIUWFOYDUX
    LUVLYDUVCUXKYBYDUVBUUNUVCYAYBUVBYCUVEUGUUQUVFQZYBYAUXKYCUVDYBUXKVQGBVPVRWHU
    WCXIRYGUVHBXQTPWOYDUVCYBUVGUVIOZUXMUWCUVCYBUVDSGEFZMUXNUVDUXOVQXRWAYGBGXSWC
    QPXT $.
    $( [2-Jan-2008] $)

  $( Two ways to state the midpoint of a segment.  (Contributed by FL,
     3-Jan-2008.) $)
  2wsms $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    ( ( A + B ) / 2 ) = ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel c2 caddc co cdiv cmul cmin wceq cc wa recn syl3anc syl2anc subadd
    3syl wb mpbid clt wbr w3a cabs cfv cc0 wne anim12i subcl abscl 2cn a1i 2ne0
    3simpa divcan2 resubcl eqeltrd 3ad2ant1 3ad2ant2 addass eqcomd addcl addcom
    simp2 2times oveq1d jca pncan cle ltle 3impia syld3an3 eqtr2d eqtr3d 3eqtrd
    abssuble0 mulcl mpan mpbird crp dmse2 rpre subdi 3eqtr4rd halfaddsubcl msr3
    simpld mulcant2i ) ACDZBCDZABUAUBZUCZEABFGZEHGZIGZEBABJGZUDUEZEHGZJGZIGZKZW
    NWSKZWLEBIGZEWRIGZJGZWMWTWOWLXEWMKZXDWMFGZXCKZWLXGXDAFGZBFGZBXIFGZXCWLXDLDZ
    ALDZBLDZXGXJKWLXDWQLWLWQLDZELDZEUFUGZXDWQKWLWPLDZWQCDZXOWLWIWJMZXMXNMZXRWIW
    JWKUNZWIXMWJXNANZBNZUHZABUIRWPUJZWQNZRXPWLUKULZXQWLUMULZWQEUOOZWLXRXSXOWLXT
    WPCDXRYBABUPWPNRYFYGRUQZWIWJXMWKYCURZWJWIXNWKYDUSZXLXMXNUCXJXGXDABUTVAOWLXI
    LDZXNXJXKKWLXLXMYNYKYLXDAVBPZYMXIBVCPWLXCBJGZXIKZXKXCKZWLYPBBFGZBJGZBXIWLXC
    YSBJWLWJXNXCYSKWIWJWKVDZYDBVERVFWLWJXNXNMYTBKUUAWJXNXNYDYDVGBBVHRWLAXDFGZBX
    IWLBAJGZXDKZUUBBKZWLXDWQUUCYJWIWJWKABVIUBZWQUUCKWIWJWKUUFABVJVKABVPVLVMWLXN
    XMXLUUDUUESYMYLYKBAXDQOTWLXMXLUUBXIKYLYKAXDVCPVNVOWLXCLDZXNYNYQYRSWLWJXNUUG
    UUAYDXPXNUUGUKEBVQVRRZYMYOXCBXIQOTVOWLUUGXLWMLDZXFXHSUUHYKWLXTYAUUIYBYEABVB
    RZXCXDWMQOVSWLXPXNWRLDZWTXEKYHYMWLWRVTDWRCDUUKABWAWRWBWRNREBWRWCOWLUUIXPXQW
    OWMKUUJYHYIWMEUOOWDWLWNLDZWSLDZXPXAXBSWLXTYAUULYBYEYAUULWPEHGLDABWEWGRWLXTW
    SCDUUMYBABWFWSNRYHWNWSEUMWHOT $.
    $( [3-Jan-2008] $)

  $( The midpoint of a segment AB of the real line is on the "right" of
     ` A ` .  (Contributed by FL, 3-Jan-2008.) $)
  msra3 $p |- ( ( A e. RR /\ B e. RR /\ A < B ) ->
    A < ( B - ( ( abs ` ( A - B ) ) / 2 ) ) ) $=
    ( cr wcel clt wbr caddc co c2 cdiv cmin cmul wa cc wceq adantr 3syl readdcl
    wb syl2anc w3a cabs cfv 3simpa recn 2cn jctir mulcom simp1 2times eqtrd jca
    simp3 pncan breq1d mpbird simp2 ltsubadd2 syl3anc mpbid eqbrtrd 3adant3 cc0
    2re 2pos pm3.2i ltmuldiv mp3an3 2wsms breqtrd ) ACDZBCDZABEFZUAZAABGHZIJHZB
    ABKHUBUCIJHKHEVNAILHZVOEFZAVPEFZVNVQAAGHZVOEVNVQIALHZVTVNVKVLMZANDZINDZMVQW
    AOVKVLVMUDZWBWCWDVKWCVLAUEZPUFUGAIUHQVNVKWCWAVTOVKVLVMUIZWFAUJQUKVNVTAKHZBE
    FZVTVOEFZVNWIVMVKVLVMUMVNWBWCWCMZWIVMSWEVKWKVLVKWCWCWFWFULPWKWHABEAAUNUOQUP
    VNVTCDZVKVLWIWJSVNVKVKWLWGWGAARTWGVKVLVMUQVTABURUSUTVAVNVKVOCDZVRVSSZWGVKVL
    WMVMABRVBVKWMICDZVCIEFZMWNWOWPVDVEVFAVOIVGVHTUTABVIVJ $.
    $( [3-Jan-2008] $)

  ${
    $d A x $.  $d x y $.
    $( Lemma for ~ iint . $)
    iintlem1 $p |- ( ( A e. RR
                          /\ y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) )
                       -> ( y e. RR -> y = A ) ) $=
      ( cr wcel crp co wceq wa wi wn cc0 adantr wb imp cxr clt wbr 3syl syl cfv
      cv cmin caddc cioo ciin wral eliin wrex cabs c2 cdiv cc wne resubcl recnd
      ancoms recn subeq0 syl2anr biimpd con3d df-ne sylibr absrpcl syl2anc 2pos
      2re elrpii rpdivcl sylancl w3a cle simplr ad2antrl simpll ltsubadd2 simp2
      wo addid1 breq2d bitrd 3exp com13 ax-mp ad2antrr impcom 3jca msra3 pm3.22
      0re adantl msr3 leloe mpbird xrlenlt mpbid intn3an2d sylancr subge0 ltlen
      orc rexr biimprcd com12 exp3a syl5bir sylbid sylbird simprll msr4 readdcl
      mslb1 rpre ex syl5 ad2antlr jca adantlr intn3an3d pm2.61ian anim2i elioo1
      mtbird jcai oveq2 oveq12d eleq2d notbid rcla4ev syl6 rexnal syl6ib syl6bi
      con4d pm2.43i impancom ) CDEZBUBZDEZYSAFCAUBZUCGZCUUAUDGZUEGZUFZEZYSCHZUU
      FYRYTIZUUGUUFUUHUUGJZUUFUUFYSUUDEZAFUGZUUIAYSFUUDUUEUHUUHUUKUUGUUHUUGUUKU
      UHUUGKZUUJKZAFUIZUUKKUUHUULYSCUCGZUJUAZUKULGZFEZYSCUUQUCGZCUUQUDGZUEGZEZK
      ZIZUUNUUHUULUVDUUHUULIZUURUVCUVEUUPFEZUKFEUURUVEUUOUMEZUUOLUNZUVFUUHUVGUU
      LUUHUUOYTYRUUODEZYSCUOZUQUPMUVEUUOLHZKZUVHUUHUULUVLUUHUVKUUGUUHUVKUUGYTYS
      UMECUMEZUVKUUGNYRYSURCURZYSCUSUTVAVBOUUOLVCVDUUOVEVFUKVHVGVIUUPUKVJVKUVEU
      URUVCUVEUURIZUVBYSPEZUUSYSQRZYSUUTQRZVLZUUOLQRZUVOUVSKUVTUVOIZUVQUVPUVRUW
      AYSUUSVMRZUVQKZUWAUWBYSUUSQRZYSUUSHZVSZUWAYTYRYSCQRZVLUWDUWFUWAYTYRUWGUVE
      YTUVTUURYRYTUULVNZVOZUVEYRUVTUURYRYTUULVPZVOUVOUVTUWGUUHUVTUWGJZUULUURYRY
      TUWKLDEZYRYTUWKJJWKYTYRUWLUWKYTYRUWLUWKYTYRUWLVLZUVTUWGUWMUVTYSCLUDGZQRUW
      GYSCLVQUWMUWNCYSQUWMYRUVMUWNCHYTYRUWLVRUVNCVTSWAWBVAWCWDWEOWFWGWHYSCWIUWD
      UWEXBSUWAYTUUSDEZUWBUWFNUWIUWAYTYRIZUWOUVOUWPUVTUUHUWPUULUURYRYTWJWFZWLYS
      CWMZTYSUUSWNVFWOUWAUVPUUSPEZUWBUWCNUVOUVPUVTUUHUVPUULUURYTUVPYRYSXCZWLWFW
      LUVOUWSUVTUVOUWPUWOUWSUWQUWRUUSXCZSWLYSUUSWPVFWQWRUVTKZUVOIZUVRUVPUVQUXCU
      UTYSVMRZUVRKZUXCUXDUUTYSQRZUUTYSHZVSZUXCYRYTCYSQRZVLUXFUXHUXCYRYTUXIUVEYR
      UXBUURUWJVOUVEYTUXBUURUWHVOZUVOUXBUXIUVEUXBUXIJUURUVEUXBLUUOVMRZUXIUVELPE
      ZUUOPEZUXKUXBNUWLUXLWKLXCWEUUHUXMUULYTYRUXMUWPUVIUXMUVJUUOXCTUQMLUUOWPWSU
      VEUXKCYSVMRZUXIUUHUXKUXNNZUULYTYRUXOYSCWTUQMUUHUULUXNUXIJZUULYSCUNZUUHUXP
      YSCVCUUHUXQUXNUXIUXQUXNIUUHUXIUXNUXQUUHUXIJUUHUXIUXNUXQICYSXAXDUQXEXFXGOX
      HXIMWGWHCYSXMUXFUXGXBSUXCUUTDEZYTUXDUXHNUXCUUHUXRUXBUUHUULUURXJCYSXKTUXJU
      UTYSWNVFWOUXCUUTPEZUVPIZUXDUXENUVOUXTUXBUUHUURUXTUULUUHUURIUXSUVPUUHUURUX
      SYRUURUXSJYTUURUUQDEZYRUXSUUQXNZYRUYAUXSYRUYAIZUXRUXSCUUQXLZUUTXCZTXOXPMO
      YTUVPYRUURUWTXQXRXSWLUUTYSWPTWQXTYAUVOUWSUXSIZUVBUVSNUVEUURUYFYRUURUYFJYT
      UULYRUURUYFYRUURIZUWSUXSUYGUYCUWOUWSUURUYAYRUYBYBZCUUQUOUXASUYGUYCUXRUXSU
      YHUYDUYESXRXOWFOUUSUUTYSYCTYDXOYEXOUUMUVCAUUQFUUAUUQHZUUJUVBUYIUUDUVAYSUY
      IUUBUUSUUCUUTUEUUAUUQCUCYFUUAUUQCUDYFYGYHYIYJYKUUJAFYLYMYOXEYNYPXEYQ $.
      $( [27-Dec-2007] $)
  $}

  ${
    $d A x $.
    $( Lemma for ~ iint . $)
    iintlem2 $p |- ( y e. |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) )
                    -> y e. RR ) $=
      ( crp cv cmin co caddc cioo ciin cr wss wrex c1 wcel ioossre wceq oveq12d
      1rp oveq2 sseq1d rcla4ev mp2an iinss ax-mp sseli ) ADCAEZFGZCUGHGZIGZJZKB
      EUJKLZADMZUKKLNDOCNFGZCNHGZIGZKLZUMSUNUOPULUQANDUGNQZUJUPKURUHUNUIUOIUGNC
      FTUGNCHTRUAUBUCADUJKUDUEUF $.
      $( [23-Dec-2007] $)
  $}


  ${
    $d A x y $.
    $( Indexed intersection of a set of open intervals centered on ` A ` .
       This theorem is a rough justification for taking finite intersections in
       the definition of a topology.  If we consider we are in the standard
       topology of ` RR ` this theorem means a non finite intersection of open
       sets can result in a closed set.  (Contributed by FL, 27-Dec-2007.) $)
    iint $p |- ( A e. RR -> |^|_ x e. RR+ ( ( A - x ) (,) ( A + x ) ) = { A } )
        $=
      ( vy cr wcel crp cv cmin co caddc cioo ciin wceq crab csn iintlem2 adantl
      wa iintlem1 jcai ex wral cbci ralrimiva eliin mpbird eleq1 syl5ibr impbid
      wi com12 eqeq1 elrab syl6bbr eqrdv rabsn eqtrd ) BDEZAFBAGZHIBUSJIKIZLZUS
      BMZADNZBOURCVAVCURCGZVAEZVDDEZVDBMZRZVDVCEURVEVHURVEVHURVERVFVGVEVFURACBP
      QACBSTUAVHURVEVGURVEUJVFURVEVGBVAEZURVIBUTEZAFUBURVJAFBUSUCUDABFUTDUEUFVD
      BVAUGUHQUKUIVBVGAVDDUSVDBULUMUNUOADBUPUQ $.
      $( [27-Dec-2007] $)
  $}

  ${
    $d A x $.
    trdom.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Domain of a translation.  (Contributed by FL, 17-Feb-2008.) $)
    trdom $p |- ( A e. RR -> dom F = RR ) $=
      ( cr wcel cv caddc co cvv wral cdm wceq readdcl elex syl ancoms ralrimiva
      wa cmpdom sylib ) BEFZAGZBHIZJFZAEKCLEMUBUEAEUCEFZUBUEUFUBSUDEFUEUCBNUDEO
      PQRAEUDCDTUA $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A x y $.
    trran.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( Range of a translation.  (Contributed by FL, 17-Feb-2008.) $)
    trran $p |- ( A e. RR -> ran F = RR ) $=
      ( vy cr wcel crn cv caddc co wceq wrex cab rnmpt wi wa expcom cc recn
      readdcl eleq1 syl5ibrcom rexlimdv cmin resubcl npcan syl2an eqcomd eqeq2d
      oveq1 rcla4ev syl2anc impbid abbi1dv syl5eq ) BFGZCHEIZAIZBJKZLZAFMZENFAE
      FUTCDOUQVBEFUQVBURFGZUQVAVCAFUSFGZUQVAVCPVDUQQVCVAUTFGUSBUAURUTFUBUCRUDVC
      UQVBVCUQQZURBUEKZFGURVFBJKZLZVBURBUFVEVGURVCURSGBSGVGURLUQURTBTURBUGUHUIV
      AVHAVFFUSVFLUTVGURUSVFBJUKUJULUMRUNUOUP $.
      $( [17-Feb-2008] $)
  $}


  ${
    $d A u v x y $.  $d F x y $.
    trnij.1 $e |- F = ( x e. RR |-> ( x + A ) ) $.
    $( A translation is 1-1-onto.  (Contributed by FL, 17-Feb-2008.) $)
    trnij $p |- ( A e. RR -> F : RR -1-1-onto-> RR ) $=
      ( vy vv vu cr wcel cv cfv wceq wral wa caddc co adantr cc recn sylanbrc
      wf1 wfo wf1o wf wi wfn crn wss wfun trdom cmpfun jctil df-fn sylibr trran
      eqimss anim2i syl2anc df-f readdcl expcom anim12d cmpt simpr fveq1i copab
      cdm imp df-mpt eleq1 eqeq1 oveq1 eqeq2d sylan9bbr anbi12d cbvopabv 3eqtri
      eqtr4i 3eqtr3g simprl ad2antrr simplrl eqid fvmpt2 simplrr simprr jca syl
      wb 3eqtr3d w3a ad2antrl ad2antll addcan2 mpbid exp31 mpd ralrimivva dff13
      3jca df-fo df-f1o ) BHIZHHCUAZHHCUBZHHCUCXCHHCUDZAJZCKZEJZCKZLZXGXILZUEZE
      HMAHMXDXCCHUFZCUGZHUHZNZXFXCXNXOHLZXQXCCUIZCVGHLZNXNXCXTXSABCDUJAHXGBOPZC
      DUKULCHUMUNZABCDUOZXRXPXNXOHUPUQURHHCUSUNXCXMAEHHXCXGHIZXIHIZNZNZYAHIZXIB
      OPZHIZNZXMXCYFYKXCYDYHYEYJYDXCYHXGBUTVAYEXCYJXIBUTVAVBVHYGYKXKXLYGYKNZXKN
      ZYAYILZXLYMXGAHYAVCZKZXIEHYIVCZKZYAYIYMXHXJYPYRYLXKVDXGCYODVEXICYQCYEFJZY
      ILZNZEFVFZYQCYOYDGJZYALZNZAGVFUUBDAGHYAVIUUEUUAAGEFXLUUCYSLZNYDYEUUDYTXLY
      DYEWIUUFXGXIHVJQUUFUUDYSYALXLYTUUCYSYAVKXLYAYIYSXGXIBOVLVMVNVOVPVQEFHYIVI
      VRVEVSYMYDYHYPYALYGYDYKXKXCYDYEVTWAYGYHYJXKWBAHYAHYOYOWCWDURYMYEYJNZYRYIL
      YLUUGXKYLYEYJXCYDYEYKWEYGYHYJWFWGQEHYIHYQYQWCWDWHWJYMXGRIZXIRIZBRIZWKZYNX
      LWIYGUUKYKXKYGUUHUUIUUJYDUUHXCYEXGSWLYEUUIXCYDXISWMXCUUJYFBSQWTWAXGXIBWNW
      HWOWPWQWRAEHHCWSTXCXNXRXEYBYCHHCXATHHCXBT $.
      $( [17-Feb-2008] $)
  $}

  ${
    $d A u v x y $.  $d A w x y z $.
    $( Converse of a translation.  (Contributed by FL, 3-Aug-2009.) $)
    cnvtr $p |- ( A e. RR -> `' ( x e. RR |-> ( x + A ) ) =
      ( x e. RR |-> ( x - A ) ) ) $=
      ( vu vy vz vw cr wcel cv caddc co cmpt wceq cfv wral oveq1 ancoms jca wfn
      wa ccnv cmin cbvmptv trnij adantr resubcl copab wb eleq1 eqeq2d sylan9bbr
      wf1o eqeq1 anbi12d cbvopabv df-mpt 3eqtr4ri a1i readdcl sylancom fvopab4g
      fveq1d recn npcan syl2anr sylan9eqr mpdan eqtrd f1ocnvfv sylc eqid fvmpt2
      cc simpr syl2anc eqtr4d ralrimiva jctil dff1o4 simprd cvv elex syl mptfng
      sylib eqidd hbmpt1 hbcnv eqfnfv2f biimpa ex biimprcd adantl impbid mpbird
      wi com12 ) BGHZAGAIZBJKZLZUAZAGWSBUBKZLZMZGGMZWSXBNZWSXDNZMZAGOZTZWRXJXFW
      RXIAGWRWSGHZTZXGXCXHXMGGXAULZXCGHZTXCXANZWSMXGXCMXMXNXOWRXNXLCBXAACGWTCIZ
      BJKWSXQBJPUCUDZUEXLWRXOWSBUFZQZRXMXPXCDGDIZBJKZLZNZWSXMXCXAYCXAYCMXMYAGHZ
      EIZYBMZTZDEUGXLFIZWTMZTZAFUGYCXAYHYKDEAFYAWSMZYFYIMZTYEXLYGYJYLYEXLUHYMYA
      WSGUIUEYMYGYIYBMYLYJYFYIYBUMYLYBWTYIYAWSBJPUJUKUNUODEGYBUPZAFGWTUPUQURVBX
      MXOXCBJKZGHZTZYDWSMXLWRYQXLWRTZXOYPXSXLWRXOYPXSXCBUSUTRQYQXMYDYOWSDEXCYBY
      OGGYCYAXCBJPYNVAXLWSVMHBVMHYOWSMWRWSVCBVCWSBVDVEVFVGVHGGXCWSXAVIVJXMXLXOX
      HXCMWRXLVNXTAGXCGXDXDVKZVLVOVPVQGVKVRWRXBGSZXDGSZXEXKUHWRXAGSZYTWRXNUUBYT
      TXRGGXAVSWEVTWRXCWAHZAGOUUAWRUUCAGXLWRUUCYRXOUUCXSXCGWBWCQVQAGXCXDYSWDWEY
      TUUATZXEXKUUDXEXKUUDXETZXFXJUUEGWFUUDXEXJAFGXBXDAFXAAFGWTWGWHAFGXCWGWIZWJ
      RWKXKUUDXEXJUUDXEWPXFUUDXEXJUUFWLWMWQWNVOWO $.
      $( [3-Aug-2009] $)
  $}

  $( The members of a 'less than or equal' relationship are extended reals.
     (Contributed by FL, 31-Jul-2009.) $)
  mlteqer $p |- ( B e. C -> ( A <_ B -> ( A e. RR* /\ B e. RR* ) ) ) $=
    ( cle wbr wcel cxr wa cop wi df-br cxp clt ccnv cdif wn eldif opelxpg sylbi
    biimpcd adantr df-le eleq2s com12 ) ABDEZBCFZAGFBGFHZUEABIZDFUFUGJZABDKUIUH
    GGLZMNZOZDUHULFUHUJFZUHUKFPZHUIUHUJUKQUMUIUNUFUMUGABGGCRTUASUBUCSUD $.
    $( [31-Jul-2009] $)

  $( Transitive law for ordering on extended reals ( compare ~ xrletr ).
     (Contributed by FL, 2-Aug-2009.) $)
  xrletr2 $p |- ( C e. D ->
             ( ( A <_ B /\ B <_ C ) -> A <_ C ) ) $=
    ( cle wbr wa wcel wi cxr mlteqer w3a xrletr exp3a 3expia com23 syl6 pm2.43i
    com3l com4l imp impcom com12 ) ABEFZBCEFZGCDHZACEFZUEUDUFUGIZUEUDUHIUFUEUEU
    DUGUFUEBJHZCJHZGUEUDUGIIZBCDKUIUJUKUDUIUJUEUGUDUIUJUEUGIZIZIUIUDUDUMUIUDAJH
    ZUIGZUDUMIABJKUOUJUDULUNUIUJUDULIUNUIUJLUDUEUGABCMNOPQSRTUAQTRUBUC $.
    $( [2-Aug-2009] $)

  ${
    $d x y $.
    $( domain of ` <_ ` is ` RR* ` .  (Contributed by FL, 2-Aug-2009.) $)
    domleqt $p |- dom <_ = RR* $=
      ( vx vy cle cdm cv wbr wex cab cxr df-dm wceq wcel wb abeq1 cvv wa wi vex
      ax-mp cpnf mlteqer simpld exlimiv pnfge pnfxr elexi breq2 imbi2d 19.37aiv
      cla4ev impbii mpgbir eqtri ) CDAEZBEZCFZBGZAHZIABCJURIKUQUNILZMAUQAINUQUS
      UPUSBUPUSUOILZUOOLUPUSUTPQBRUNUOOUASUBUCUSUPBUSUNTCFZQZUSUPQZBGUNUDVCVBBT
      TIUEUFUOTKUPVAUSUOTUNCUGUHUJSUIUKULUM $.
      $( [2-Aug-2009] $)
  $}

  $( The range of ` <_ ` is included in the extended reals.  (Contributed by
     FL, 2-Aug-2009.) $)
  ranleqt $p |- ran <_ C_ RR* $=
    ( cle crn cxr cxp clt ccnv cdif df-le rneqi difss rnss ax-mp rnxpid sseqtri
    wss eqsstri ) ABCCDZEFZGZBZCASHITQBZCSQOTUAOQRJSQKLCMNP $.
    $( [2-Aug-2009] $)

  $( 'Less or equal to' is a relation.  (Contributed by FL, 2-Aug-2009.) $)
  leqrl $p |- Rel <_ $=
    ( cle wrel cxr cxp clt ccnv cdif relxp reldif ax-mp df-le releqi mpbir ) AB
    CCDZEFZGZBZNBQCCHNOIJAPKLM $.
    $( [2-Aug-2009] $)

  $( The field of the 'less or equal to' relationship on the extended real.
     (Contributed by FL, 2-Aug-2009.) $)
  fldleqt $p |- U. U. <_ = RR* $=
    ( cle cuni cdm crn cun cxr wrel wceq leqrl relfld ax-mp wcel domleqt eleq2i
    vx cv wo biimpi ranleqt sseli jaoi biimpri orcd impbii uneqri eqtri ) ABBZA
    CZADZEZFAGUGUJHIAJKOUHUIFOPZUHLZUKUILZQUKFLZULUNUMULUNUHFUKMNZRUIFUKSTUAUNU
    LUMULUNUOUBUCUDUEUF $.
    $( [2-Aug-2009] $)

  ${
    $d x y z $.
    $( The "less than or equal to" relationship on the extended reals is a
       toset.  (Contributed by FL, 2-Aug-2009.  Modified 10-Nov-2014.) $)
    lteqttos $p |- <_ e. TosetRel $=
      ( vx vy vz cle wcel cxr ccnv wss wa cvv xrex ax-mp cuni wceq cv wbr mpbir
      wi wal pm2.43i ctsr cps cxp cun clt cdif df-le xpex eqeltri wrel ccom cin
      difexg cid cres w3a leqrl vex xrletr2 ax-gen gen2 cotr wb asymref mlteqer
      xrletri3 biimprd exp3acom23 ancoms syl6 com4l impcom com12 eleq1 ex com3l
      biimpd syl6bi pm2.43a com13 impbid alrimiv fldleqt eleq2s mprgbir 3pm3.2i
      isps mpbiri wo wral xrletri rgen2 qfto pm3.2i eqcomi istoset ) DUAEDUBEZF
      FUCZDDGZUDHZIWQWTDJEZWQDWRUEGZUFZJUGWRJEXCJEFFKKUHWRXBJUMLUIXAWQDUJZDDUKD
      HZDWSULUNDMMZUONZUPXDXEXGUQXEAOZBOZDPZXICOZDPIXHXKDPRZCSZBSASXMABXLCXKJEX
      LCURXHXIXKJUSLUTVAABCDVBQXGXJXIXHDPZIZXHXINZVCZBSZAXFABDVDXRXHFXFXHFEZXQB
      XSXOXPXOXSXPXNXJXSXPRZXNXJXTRXSXNXNXJXPXSXNXIFEZXSIXNXJXPRRZXIXHFVEXSYAYB
      XSYAIZXJXNXPYCXPXOXHXIVFZVGVHVIVJVKTVLVMXSXPXORZXPXSXSXOXSXPXSXORZXPXSYAX
      PYFRXHXIFVNXSYAXPXOXSYAYEYCXPXOYDVQVOVPVRVSVTTWAWBWCWDWEWFJDWGWHLWTXJXNWI
      ZBFWJAFWJYGABFFXHXIWKWLABFFDWMQWNDFXFFWCWOWPQ $.
      $( [17-Nov-2014] $)


  $}


