$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Topological groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c TopGrp $.

  $( Extend class notation with the class of all topological groups. $)
  ctopgrp $a class TopGrp $.

  ${
    $d f g j i $.
    $( Define the class of all topological groups.  A topological group is a
       group whose operation and inverse function are continuous. $)
    df-topgrp $a |- TopGrp = { f e. ( Grp i^i TopSp ) |
     [ ( +g ` f ) / g ] [ ( TopOpen ` f ) / j ] [ ( invg ` f ) / i ]
       ( g e. ( ( j tX j ) Cn j ) /\ i e. ( j Cn j ) ) } $.
  $}

  ${
    $d f g i j G $.  $d g i j I $.  $d g j J $.  $d g j P $.
    istopgrp.1 $e |- P = ( +g ` G ) $.
    istopgrp.2 $e |- J = ( TopOpen ` G ) $.
    istopgrp.3 $e |- I = ( invg ` G ) $.
    $( The predicate "is a topological group".  Bourbaki TG III.1 (Contributed
       by FL, 18-Apr-2010.  Revised 20-Jun-2014 by FL and MC.) $)
    istopgrp $p |- ( G e. TopGrp <-> ( G e. Grp /\ G e. TopSp /\
      ( P e. ( ( J tX J ) Cn J ) /\ I e. ( J Cn J ) ) ) ) $=
      ( vg vj vi wcel co ccn wa cfv wsbc wceq wb wi ex vf ctopgrp cgrp ctps cin
      ctx w3a cv cminusg cts cplusg crab df-topgrp eleq2i fveq2 dfsbcq syl fvex
      cvv sbcbidv com12 ax-mp bitrd elrab eqcoms eleq1d anbi2d sbcie a1i adantr
      eqtr oveq12d adantl eleq12d eleq2d sbc2ie anbi2i bitri elin anbi1i df-3an
      anbi12d bicomi ) BUBKZBUCUDUEZKZADDUFLZDMLZKZCDDMLZKZNZNZBUCKZBUDKZWLUGZW
      DBHUHZIUHZWRUFLZWRMLZKZJUHZWRWRMLZKZNZJUAUHZUIOZPZIXFUJOZPZHXFUKOZPZUAWEU
      LZKZWMUBXMBUAHJIUMUNXNWFXEJBUIOZPZIBUJOZPZHBUKOZPZNWMXLXTUABWEXFBQZXLXJHX
      SPZXTYAXKXSQXLYBRXFBUKUOXJHXKXSUPUQXSUSKZYAYBXTRZSBUKURZYAYCYDYAYCYDYAXJX
      RHXSUSYAXJXHIXQPZXRYAXIXQQXJYFRXFBUJUOXHIXIXQUPUQXQUSKZYAYFXRRZSBUJURZYAY
      GYHYAYGYHYAXHXPIXQUSYAXGXOQXHXPRXFBUIUOXEJXGXOUPUQUTTVAVBVCUTTVAVBVCVDXTW
      LWFXPWLHIXSXQYEYIWQXSQZWRXQQZNZXPXACXCKZNZWLXPYNRYLXEYNJXOBUIURXBXOQZXDYM
      XAYOXBCXCCXOQYOXBCQZSZGYQXOCYOXOCQZYPYOYRYPXBXOCVKTVAVEVBVFVGVHVIYLXAWIYM
      WKYLWQAWTWHYJWQAQZYKAXSQYJYSSZEYTXSAYJXSAQZYSYJUUAYSWQXSAVKTVAVEVBVJYKWTW
      HQYJYKWSWGWRDMDXQQZYKWSWGQZSZFUUDXQDYKXQDQZUUCYKUUEUUCYKUUENWRDWRDUFWRXQD
      VKZUUFVLTVAVEVBUUBYKWRDQZSZFUUHXQDYKUUEUUGYKUUEUUGUUFTVAVEVBZVLVMVNYKYMWK
      RYJYKXCWJCYKWRDWRDMUUIUUIVLVOVMWBVCVPVQVRVRWMWNWONZWLNZWPWFUUJWLBUCUDVSVT
      WPUUKWNWOWLWAWCVRVR $.
      $( [17-Nov-2014] $)

    $( A topological group is a group.  (Contributed by FL, 18-Apr-2010.
       Revised 21-Jun-2014.) $)
    topgrpgrp $p |- ( G e. TopGrp -> G e. Grp ) $=
      ( ctopgrp wcel cgrp ctps ctx co ccn wa istopgrp simp1bi ) BHIBJIBKIADDLMD
      NMICDDNMIOABCDEFGPQ $.
      $( [27-Jun-2014] $)


    $( A topological group is a topological space.  (Contributed by FL,
       21-Jun-2010.  Revised 21-Jun-2014.) $)
    topgrptop $p |- ( G e. TopGrp -> G e. TopSp ) $=
      ( ctopgrp wcel cgrp ctps ctx co ccn wa istopgrp simp2bi ) BHIBJIBKIADDLMD
      NMICDDNMIOABCDEFGPQ $.
      $( [27-Jun-2014] $)


    $( In a topological group, the operation ` P ` is continuous.  (Contributed
       by FL, 21-Jun-2010.  Revised 21-Jun-2014.) $)
    topgrpcn $p |- ( G e. TopGrp -> P e. ( ( J tX J ) Cn J ) ) $=
      ( ctopgrp wcel ctx co ccn cgrp ctps wa istopgrp simp3bi simpld ) BHIZADDJ
      KDLKIZCDDLKIZSBMIBNITUAOABCDEFGPQR $.
      $( [27-Jun-2014] $)


    $( In a topological group, the inverse function is continuous.
       (Contributed by FL, 21-Jun-2010.  Revised 21-Jun-2014.) $)
    topgrpinv $p |- ( G e. TopGrp -> I e. ( J Cn J ) ) $=
      ( ctopgrp wcel ctx co ccn cgrp ctps wa istopgrp simp3bi simprd ) BHIZADDJ
      KDLKIZCDDLKIZSBMIBNITUAOABCDEFGPQR $.
      $( [27-Jun-2014] $)

  $}

  ${
    istpstp.j $e |- J = ( TopOpen ` K ) $.
    $( Express the predicate "is a topological space." $)
    istpstp $p |- ( K e. TopSp -> J e. Top ) $=
      ( ctps wcel ctop cbs cfv cuni wceq eqid istps simplbi ) BDEAFEBGHZAIJNABN
      KCLM $.
      $( [27-Jun-2014] $)
  $}

  ${
    istpsbas.a $e |- A = ( Base ` K ) $.
    istpsbas.j $e |- J = ( TopOpen ` K ) $.
    $( Express the predicate "is a topological space." $)
    istpsbas $p |- ( K e. TopSp -> A = U. J ) $=
      ( ctps wcel ctop cuni wceq istps simprbi ) CFGBHGABIJABCDEKL $.
      $( [27-Jun-2014] $)
  $}


  ${
    $d x y G $.  $d x y J $.
    topgrpsubcn.2 $e |- J = ( TopOpen ` G ) $.
    topgrpsubcn.3 $e |- M = ( -g ` G ) $.
    $( In a topological group, the "subtraction" (or "division") is
       continuous.  Bourbaki TG III.1 axiom GT' (Contributed by FL,
       21-Jun-2010.)  (The proof was shortened by Mario Carneiro,
       30-May-2014.)  (Revised by FL, 21-Jun-2014.) $)
    topgrpsubcn $p |- ( G e. TopGrp -> M e. ( ( J tX J ) Cn J ) ) $=
      ( vx vy ctopgrp wcel cbs cfv cv cminusg cplusg cmpt2 eqid syl wceq wral
      co ctx ccn grpsubfval cuni ctps ctop topgrptop istpstp cnmpt1st topgrpinv
      cnmpt2nd cnmpt21f topgrpcn cnmpt22f wb istpsbas id eqidd rgen2a mpt2eq123
      wa a1i w3a eleq1d mpd3an23 mpbird eqeltrd ) AHIZCFGAJKZVIFLZGLZAMKZKZANKZ
      TZOZBBUATBUBTZFGVIVNAVLCHVIPZVNPZVLPZEUCVHVPVQIZFGBUDZWBVOOZVQIZVHFGVJVMV
      NBBBBBWBWBWBWBWBPZWEVHAUEIZBUFIVNAVLBVSDVTUGZBADUHQZWHWHWHVHFGBBWBWBWEWEW
      HWHUIVHFGVKVLBBBBWBWBWEWEWHWHWHWHVHFGBBWBWBWEWEWHWHUKVNAVLBVSDVTUJULWEWEW
      HVNAVLBVSDVTUMUNVHWFWAWDUOZWGWFVIWBRZWIVIBAVRDUPWJWJVOVORZGVISFVISZWIWJUQ
      WLWJWKFGVIVJVIIVKVIIVAVOURUSVBWJWJWLVCVPWCVQFGVIVIVOWBWBVOUTVDVEQQVFVG $.
      $( [27-Jun-2014] $)
  $}

  $c opfn $.

  $( Extend class notation with an operator that derives an operation on
     functions from an operation on the elements of the common range of those
     functions. $)
  copfn $a class opfn $.


  ${
    $d g i h x y z a b $.
    $( Multiplication or addition of two functions ` x ` and ` y ` derived from
       the operation ` g ` on the elements of the common range of ` x ` and
       ` y ` .  The functions ` x ` and ` y ` must also have the same domain
       ` i ` . $)
    df-opfn $a |- opfn = { <. <. g , i >. , h >. |
       h = { <. <. x , y >. , z >. |
         ( x e. ( dom dom g ^m i ) /\ y e. ( dom dom g ^m i ) /\
       z = { <. a , b >. | ( a e. dom x /\ b = ( ( x ` a ) g ( y ` a ) ) ) } )
        }
        } $.
  $}

