$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Lattice (algebraic definition)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c LatAlg $.

  $( Extend class notation to include the class of lattices. $)
  clatalg $a class LatAlg $.

  ${
    $d j m t x y z $.
    $( Algebraic definition of a lattice. ` j ` is called the join of the
       lattice (and is denoted by ` \/ ` in textbooks) , ` m ` is called the
       meet (and is denoted by ` /\ ` in textbooks). $)
    df-latalg $a |- LatAlg = { <. j , m >. | E. t ( j : ( t X. t ) --> t /\
    m : ( t X. t ) --> t /\
    A. x e. t A. y e. t ( ( x j y ) = ( y j x ) /\ ( x m y ) = ( y m x ) /\
     ( ( x m ( x j y ) ) = x /\ ( x j ( x m y ) ) = x /\
     A. z e. t ( ( x m ( y m z ) ) = ( ( x m y ) m z ) /\
                 ( x j ( y j z ) ) = ( ( x j y ) j z ) ) ) ) ) } $.
  $}

  ${
    $d J j m t x y z $.  $d M j m x y z $.  $d X j m t x y z $.
    islatalg.1 $e |- X = dom dom J $.
    $( The predicate "is a lattice".  (Contributed by FL, 11-Dec-2009.) $)
    islatalg $p |- ( ( J e. A /\ M e. B ) -> ( <. J , M >. e. LatAlg <->
   ( J : ( X X. X ) --> X /\ M : ( X X. X ) --> X /\
    A. x e. X A. y e. X ( ( x J y ) = ( y J x ) /\ ( x M y ) = ( y M x ) /\
     ( ( x M ( x J y ) ) = x /\ ( x J ( x M y ) ) = x /\
     A. z e. X ( ( x M ( y M z ) ) = ( ( x M y ) M z ) /\
                 ( x J ( y J z ) ) = ( ( x J y ) J z ) ) ) ) ) ) ) $=
      ( vt wcel wa cv wf co wceq wral w3a oveq cvv vj cop clatalg cxp wex copab
      df-latalg a1i eleq2d cdm fdm dmxpid dmeq syl5eqr eqcoms 3ad2ant1 pm4.71ri
      vm wb syl exbii dmeqd eqeq2d feq1 eqeq12d oveq2d eqeq1d id eqidd oveq123d
      anbi2d ralbidv 3anbi123d 3anbi13d anbi12d exbidv eqcomi eqeq2i anbi1d vex
      2ralbidv bitrd dmex eleq1 biimpd eleq1i syl6ib 3syl xpeq12 anidms mpancom
      mpi feq23 raleq 3anbi3d raleqbi1dv ceqsexgv 3bitrd 3anbi23d opelopabg ) F
      DKGEKLZFGUBZUCKXBJMZXCUDZXCUAMZNZXDXCURMZNZAMZBMZXEOZXJXIXEOZPZXIXJXGOZXJ
      XIXGOZPZXIXKXGOZXIPZXIXNXEOZXIPZXIXJCMZXGOZXGOZXNYAXGOZPZXIXJYAXEOZXEOZXK
      YAXEOZPZLZCXCQZRZRZBXCQAXCQZRZJUEZUAURUFZKHHUDZHFNZYRHGNZXIXJFOZXJXIFOZPZ
      XIXJGOZXJXIGOZPZXIUUAGOZXIPZXIUUDFOZXIPZXIXJYAGOZGOZUUDYAGOZPZXIXJYAFOZFO
      ZUUAYAFOZPZLZCHQZRZRZBHQAHQZRZXAUCYQXBUCYQPXAABCJUAURUGUHUIYPYSYRHXGNZUUC
      XPXIUUAXGOZXIPZXIXNFOZXIPZYEUURLZCHQZRZRZBHQZAHQZRZUVDUAURFGDEXEFPZYPXCXE
      UJZUJZPZYOLZJUEZXCHPZXDXCFNZXHUUCXPUVGUVIUVJCXCQZRZRZBXCQZAXCQZRZLZJUEZUV
      PYPUWBUSUVQYOUWAJYOUVTXFXHUVTYNXFUVRXDPUVTXDXCXEUKUVTXDUVRXDUVRPXCXDUJUVS
      XCULXDUVRUMUNUOUTUPUQVAUHUVQUWBXCFUJZUJZPZUWJLZJUEUWLUVQUWAUWPJUVQUVTUWOY
      OUWJUVQUVSUWNXCUVQUVRUWMXEFUMZVBVCUVQXFUWDYNUWIXHXDXCXEFVDUVQYMUWGABXCXCU
      VQXMUUCYLUWFXPUVQXKUUAXLUUBXIXJXEFSZXJXIXEFSVEUVQXRUVGXTUVIYKUWEUVQXQUVFX
      IUVQXKUUAXIXGUWRVFVGUVQXSUVHXIXIXNXEFSVGUVQYJUVJCXCUVQYIUURYEUVQYGUUPYHUU
      QUVQXIXIYFUUOXEFUVQVHZUVQXIVIXJYAXEFSVJUVQXKUUAYAYAXEFUWSUWRUVQYAVIVJVEVK
      VLVMVNWAVNVOVPUVQUWPUWKJUVQUWOUWCUWJUWOUWCUSUVQUWNHXCHUWNIVQZVRUHVSVPWBUV
      QHTKZUWLUVPUSUVQUVRUWMPUVSUWNPZUXAUWQUVRUWMUMUXBUVSTKZUXAUVRXEUAVTWCWCUXB
      UXCUWNTKZUXAUXBUXCUXDUVSUWNTWDWEUWNHTUWTWFWGWLWHUWJUVPJHTUWCUWDYSXHUVEUWI
      UVOXDYRPZUWCUWDYSUSUWCUXEXCHXCHWIWJZXDXCYRHFWMWKUXEUWCXHUVEUSUXFXDXCYRHXG
      WMWKUWHUVNAXCHUWGUVMBXCHUWCUWFUVLUUCXPUWCUWEUVKUVGUVIUVJCXCHWNWOWOWPWPVMW
      QUTWRXGGPZUVEYTUVOUVCYSYRHXGGVDUXGUVMUVBABHHUXGXPUUFUVLUVAUUCUXGXNUUDXOUU
      EXIXJXGGSZXJXIXGGSVEUXGUVGUUHUVIUUJUVKUUTUXGUVFUUGXIXIUUAXGGSVGUXGUVHUUIX
      IUXGXNUUDXIFUXHVFVGUXGUVJUUSCHUXGYEUUNUURUXGYCUULYDUUMUXGXIXIYBUUKXGGUXGV
      HZUXGXIVIXJYAXGGSVJUXGXNUUDYAYAXGGUXIUXHUXGYAVIVJVEVSVLVMWSWAWSWTWB $.
      $( [11-Dec-2009] $)
  $}

  ${
    $d J x y z $.  $d M x y z $.  $d X x y z $.
    jop1 $e |- X = dom dom J $.
    ${
      $( Join is a binary internal operation.  (Contributed by FL,
         12-Dec-2009.) $)
      jop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> J : ( X X. X ) --> X ) $=
        ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a syl6bi 3impia
        islatalg simp1 ) CAJZDBJZCDKLJZEEMZECNZUEUFOUGUIUHEDNZGPZHPZCQZULUKCQRU
        KULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIEST
        THESGESZTUIGHIABCDEFUCUIUJUPUDUAUB $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Meet is a binary internal operation.  (Contributed by FL,
         12-Dec-2009.) $)
      mop $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> M : ( X X. X ) --> X ) $=
        ( vx vy vz wcel cop clatalg cxp wf wa cv co wceq wral w3a syl6bi 3impia
        islatalg simp2 ) CAJZDBJZCDKLJZEEMZEDNZUEUFOUGUHECNZUIGPZHPZCQZULUKCQRU
        KULDQZULUKDQRUKUMDQUKRUKUNCQUKRUKULIPZDQDQUNUODQRUKULUOCQCQUMUOCQROIEST
        THESGESZTUIGHIABCDEFUCUJUIUPUDUAUB $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Closure of join.  (Contributed by FL, 12-Dec-2009.) $)
      cljo $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P J Q ) e. X ) ) $=
        ( wcel cop clatalg w3a wa cxp wf co jop adantr simprl simprr 3jca fovrn
        ex syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGEOZUFUGLZCDEPGIUEUHUJUEUHMUIUFUGUE
        UIUHABEFGHQRUEUFUGSUEUFUGTUAUCCDGGGEUBUD $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Closure of meet.  (Contributed by FL, 12-Dec-2009.) $)
      clme $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> ( ( P e. X /\ Q e. X ) -> ( P M Q ) e. X ) ) $=
        ( wcel cop clatalg w3a wa cxp wf co mop adantr simprl simprr 3jca fovrn
        ex syl6 ) EAIFBIEFJKILZCGIZDGIZMZGGNGFOZUFUGLZCDFPGIUEUHUJUEUHMUIUFUGUE
        UIUHABEFGHQRUEUFUGSUEUFUGTUAUCCDGGGFUBUD $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Absorption law. ` ( x /\ ( x \/ y ) ) = x ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labs1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
         -> A. x e. X A. y e. X ( x M ( x J y ) ) = x ) $=
        ( vz wcel cop clatalg cv co wceq wral wa wf w3a ralimi islatalg simp31
        cxp 3ad2ant3 syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZENZFNUJOZBGPZAGPZUGU
        HQUIGGUCZGERZUPGFRZULUKUJENOZUJUKFNZUKUJFNOZUMUJUTENUJOZUJUKIMZFNFNUTVC
        FNOUJUKVCENENULVCENOQIGPZSSZBGPZAGPZSUOABICDEFGHUAVGUQUOURVFUNAGVEUMBGU
        SVAUMVBVDUBTTUDUEUF $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y $.  $d M x y $.  $d P x y $.  $d Q y $.  $d X x y $.
      $( Absorption law. ` ( P /\ ( P \/ Q ) ) = P ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labss1 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> ( ( P e. X /\ Q e. X ) -> ( P M ( P J Q ) ) = P ) ) $=
        ( vx vy wcel cop clatalg w3a cv co wceq wral wa labs1 id oveq12d oveq2d
        oveq1 eqeq12d oveq2 eqeq1d rcla42v syl5com ) EAKFBKEFLMKNIOZUJJOZEPZFPZ
        UJQZJGRIGRCGKDGKSCCDEPZFPZCQZIJABEFGHTUNUQCCUKEPZFPZCQIJCDGGUJCQZUMUSUJ
        CUTUJCULURFUTUAZUJCUKEUDUBVAUEUKDQZUSUPCVBURUOCFUKDCEUFUCUGUHUI $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y z $.  $d M x y z $.  $d X x y z $.
      $( Absorption law. ` ( x \/ ( x /\ y ) ) = x ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labs2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> A. x e. X A. y e. X ( x J ( x M y ) ) = x ) $=
        ( vz wcel cop clatalg cv co wceq wral wa wf w3a ralimi islatalg simp32
        cxp 3ad2ant3 syl6bi 3impia ) ECJZFDJZEFKLJZAMZUJBMZFNZENUJOZBGPZAGPZUGU
        HQUIGGUCZGERZUPGFRZUJUKENZUKUJENOZULUKUJFNOZUJUSFNUJOZUMUJUKIMZFNFNULVC
        FNOUJUKVCENENUSVCENOQIGPZSSZBGPZAGPZSUOABICDEFGHUAVGUQUOURVFUNAGVEUMBGU
        TVAVBUMVDUBTTUDUEUF $.
        $( [12-Dec-2009] $)
    $}

    ${
      $d J x y $.  $d M x y $.  $d P x y $.  $d Q y $.  $d X x y $.
      $( Absorption law. ` ( P \/ ( P /\ Q ) ) = P ` .  (Contributed by FL,
         12-Dec-2009.) $)
      labss2 $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg )
           -> ( ( P e. X /\ Q e. X ) -> ( P J ( P M Q ) ) = P ) ) $=
        ( vx vy wcel cop clatalg w3a cv co wceq wral wa labs2 id oveq12d oveq2d
        oveq1 eqeq12d oveq2 eqeq1d rcla42v syl5com ) EAKFBKEFLMKNIOZUJJOZFPZEPZ
        UJQZJGRIGRCGKDGKSCCDFPZEPZCQZIJABEFGHTUNUQCCUKFPZEPZCQIJCDGGUJCQZUMUSUJ
        CUTUJCULUREUTUAZUJCUKFUDUBVAUEUKDQZUSUPCVBURUOCEUKDCFUFUCUGUHUI $.
        $( [12-Dec-2009] $)
    $}

    $( Join is idempotent. ` ( P \/ P ) = P ` .  (Contributed by FL,
       12-Dec-2009.) $)
    jidd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P J P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa labss1 imp simpl simprl sylc cljo ex
      jca labss2 oveq2 eqtr eqcoms syl expcom anidms com12 ) CFHZDAHEBHDEIJHKZC
      CDLZCMZULUMUONUMULULOZUOUMUPOZCUNELZCMZCURDLZCMZUOUMUPUSABCCDEFGPQUQUMULU
      NFHZOVAUMUPRUQULVBUMULULSUMUPVBABCCDEFGUAQUCABCUNDEFGUDTUSUTUNMVAUONZURCC
      DUEVCUNUTUNUTMVAUOUNUTCUFUBUGUHTUIUJUK $.
      $( [12-Dec-2009] $)

    $( Meet is idempotent. ` ( P /\ P ) = P ` .  (Contributed by FL,
       12-Dec-2009.) $)
    midd $p |- ( ( J e. A /\ M e. B /\ <. J , M >. e. LatAlg ) ->
      ( P e. X -> ( P M P ) = P ) ) $=
      ( wcel cop clatalg w3a co wceq wi wa labss2 imp simpl simprl sylc clme ex
      jca labss1 oveq2 eqtr eqcoms syl expcom anidms com12 ) CFHZDAHEBHDEIJHKZC
      CELZCMZULUMUONUMULULOZUOUMUPOZCUNDLZCMZCURELZCMZUOUMUPUSABCCDEFGPQUQUMULU
      NFHZOVAUMUPRUQULVBUMULULSUMUPVBABCCDEFGUAQUCABCUNDEFGUDTUSUTUNMVAUONZURCC
      EUEVCUNUTUNUTMVAUOUNUTCUFUBUGUHTUIUJUK $.
      $( [12-Dec-2009] $)
  $}

