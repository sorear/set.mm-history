$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Subcategories
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c SubCat $.
  $( Extend class notation with a function returning all the subcategories of a
     given category. $)
  csubc $a class SubCat $.

  ${
    $d x y a $.
    $( ` ( SubCat `` x ) ` is the set of all the subcategories of the category
       ` x ` .  All the objects and all the morphisms of the subcategory belong
       to the supercategory.  The identity of an object, the domain and the
       codomain of a morphism are the same in the subcategory and the
       supercategory.  The composition of the subcategory is a restriction of
       the composition of the supercategory. $)
    df-subc $a |- SubCat = { <. x , y >. | ( x e. Cat /\ y = { a e. Cat | (
    ( id_ ` a ) C_ ( id_ ` x ) /\ ( ( dom_ ` a ) C_ ( dom_ ` x )
    /\ ( cod_ ` a ) C_ ( cod_ ` x ) ) /\ ( o_ ` a ) C_ ( o_ ` x ) ) } ) } $.
  $}

  ${
    $d C a u $.  $d C a x y $.  $d D a u $.  $d D a x y $.  $d J a u $.
    $d J a x y $.  $d R a u $.  $d R a x y $.  $d T a x y $.
    issubcat.2 $e |- D = ( dom_ ` T ) $.
    issubcat.3 $e |- C = ( cod_ ` T ) $.
    issubcat.4 $e |- R = ( o_ ` T ) $.
    issubcat.5 $e |- J = ( id_ ` T ) $.
    $( The set of all the subcategories of ` T ` .  (Contributed by FL,
       17-Sep-2009.) $)
    issubcat $p |- ( T e. Cat -> ( SubCat ` T ) = { a e. Cat | (
    ( id_ ` a ) C_ J /\ ( ( dom_ ` a ) C_ D
    /\ ( cod_ ` a ) C_ C ) /\ ( o_ ` a ) C_ R ) } ) $=
      ( cid_ cfv wss cdom_ ccod_ wa co_ ccat fveq2 wcel vx vy vu w3a crab csubc
      cv wceq syl6eqr sseq2d anbi12d 3anbi123d rabbidv df-subc cpw cxp cvv fvex
      eqeltri pwex xpex c1st c2nd cop wrel relcat 1st2nd mpan cdm reldcat 1stdm
      adantr sylancr wi elpw eqid domval eleq1i ax-1 sylbir a1d a1i 3imp impcom
      sylbi codval adantl 3ad2ant2 jca32 crn relrcat 2ndrn idval simp1 jca 3exp
      cmpval elxp6 sseq1d elrab anbi1i anbi2i bitri 3imtr4i ssriv ssexi fvopab4
      sylanbrc ) UAUBDFUGZKLZUAUGZKLZMZXINLZXKNLZMZXIOLZXKOLZMZPZXIQLZXKQLZMZUD
      ZFRUEXJEMZXNBMZXQAMZPZYACMZUDZFRUEZRUFXKDUHZYDYJFRYLXMYEXTYHYCYIYLXLEXJYL
      XLDKLZEXKDKSJUIUJYLXPYFXSYGYLXOBXNYLXODNLZBXKDNSGUIUJYLXRAXQYLXRDOLZAXKDO
      SHUIUJUKYLYBCYAYLYBDQLZCXKDQSIUIUJULUMUAUBFUNYKBUOZAUOZUPZEUOZCUOZUPZUPZY
      SUUBYQYRBBYNUQGDNURUSUTAAYOUQHDOURUSUTVAYTUUAEEYMUQJDKURUSUTCCYPUQIDQURUS
      UTVAVAUCYKUUCUCUGZRTZUUDKLZEMZUUDNLZBMZUUDOLZAMZPZUUDQLZCMZUDZPZUUDUUDVBL
      ZUUDVCLZVDUHZUUQUUQVBLZUUQVCLZVDUHZUUTYQTZUVAYRTZPPZUURUUBTZPZPZUUDYKTUUD
      UUCTZUUPUUSUVEUVFUUEUUSUUORVEZUUEUUSVFUUDRVGVHVLUUPUVBUVCUVDUUEUVBUUOUUER
      VIZVEUUQUVKTZUVBVJUVJUUEUVLVFUUDRVKVHUUQUVKVGVMVLUUOUUEUVCUUGUULUUNUUEUVC
      VNZUULUUNUVMVNZVNUUGUUIUVNUUKUUIUVMUUNUUIUUHYQTZUVMUUHBUUDNURVOUVOUVCUVMU
      UHUUTYQUUHUUDUUHVPVQVRUVCUUEVSWEVTWAVLWBWCWDUUOUUEUVDUULUUGUUEUVDVNZUUNUU
      KUVPUUIUUKUUJYRTZUVPUUJAUUDOURVOUVQUVDUVPUUJUVAYRUUJUUDUUJVPWFVRUVDUUEVSW
      EVTWGWHWDWIUUPUURUURVBLZUURVCLZVDUHZUVRYTTZUVSUUATZPZUVFUUEUVTUUOUUERWJZV
      EUURUWDTZUVTWKUVJUUEUWEVFUUDRWLVHUURUWDVGVMVLUUOUUEUWCUUGUULUUNUUEUWCVNZU
      UGUUNUWFVNZUULUUGUUFYTTZUWGUUFEUUDKURVOUWHUWAUWGUUFUVRYTUUDUUFUUFVPWMVRUW
      AUUNUUEUWCUWAUUNUUEUDUWAUWBUWAUUNUUEWNUWAUUNUUEUWBUUNUUEUWBVNZVNUWAUUNUUM
      UUATZUWIUUMCUUDQURVOUWJUWBUWIUUMUVSUUAUUDUUMUUMVPWQVRUWBUUEVSWEVTWBWCWOWP
      WEVTWAWCWDUURYTUUAWRXHWIYJUUOFUUDRXIUUDUHZYEUUGYHUULYIUUNUWKXJUUFEXIUUDKS
      WSUWKYFUUIYGUUKUWKXNUUHBXIUUDNSWSUWKXQUUJAXIUUDOSWSUKUWKYAUUMCXIUUDQSWSUL
      WTUVIUUSUUQYSTZUVFPZPUVHUUDYSUUBWRUWMUVGUUSUWLUVEUVFUUQYQYRWRXAXBXCXDXEXF
      XG $.
      $( [27-Dec-2014] $) $( [17-Sep-2009] $)
  $}

  ${
    $d C a $.  $d D a $.  $d J a $.  $d R a $.  $d T a $.  $d U a $.
    issubcata.2 $e |- D = ( dom_ ` T ) $.
    issubcata.3 $e |- C = ( cod_ ` T ) $.
    issubcata.4 $e |- R = ( o_ ` T ) $.
    issubcata.5 $e |- J = ( id_ ` T ) $.
    $( The predicate "is a subcategory of" ` T ` .  (Contributed by FL,
       17-Sep-2009.) $)
    issubcata $p |- ( T e. Cat -> ( U e. ( SubCat ` T ) <-> (
    U e. Cat /\ ( ( id_ ` U ) C_ J /\ ( ( dom_ ` U ) C_ D
    /\ ( cod_ ` U ) C_ C ) /\ ( o_ ` U ) C_ R ) ) ) ) $=
      ( va ccat wcel cfv cid_ wss cdom_ ccod_ fveq2 sseq1d csubc cv wa co_ crab
      w3a issubcat eleq2d wceq anbi12d 3anbi123d elrab syl6bb ) DLMZEDUANZMEKUB
      ZONZFPZUPQNZBPZUPRNZAPZUCZUPUDNZCPZUFZKLUEZMELMEONZFPZEQNZBPZERNZAPZUCZEU
      DNZCPZUFZUCUNUOVGEABCDFKGHIJUGUHVFVQKELUPEUIZURVIVCVNVEVPVRUQVHFUPEOSTVRU
      TVKVBVMVRUSVJBUPEQSTVRVAVLAUPERSTUJVRVDVOCUPEUDSTUKULUM $.
      $( [17-Sep-2009] $)
  $}

  ${
    $d C a $.  $d D a $.  $d J a $.  $d R a $.  $d T a $.  $d U a $.
    issubcatb.2 $e |- D = ( dom_ ` T ) $.
    issubcatb.3 $e |- C = ( cod_ ` T ) $.
    issubcatb.4 $e |- R = ( o_ ` T ) $.
    issubcatb.5 $e |- J = ( id_ ` T ) $.
  $( Todo: to be removed. $)
    $( The predicate "is a subcategory of" ` T ` .  (Contributed by FL,
       17-Sep-2009.) $)
    issubcatb $p |- ( ( T e. Cat /\ U e. Cat ) -> ( U e. ( SubCat ` T ) <-> (
    ( id_ ` U ) C_ J /\ ( ( dom_ ` U ) C_ D
    /\ ( cod_ ` U ) C_ C ) /\ ( o_ ` U ) C_ R ) ) ) $=
      ( va ccat wcel cfv cid_ wss cdom_ ccod_ fveq2 sseq1d csubc cv wa co_ crab
      w3a issubcat eleq2d wceq anbi12d 3anbi123d elrab3 sylan9bb ) DLMZEDUANZME
      KUBZONZFPZUPQNZBPZUPRNZAPZUCZUPUDNZCPZUFZKLUEZMELMEONZFPZEQNZBPZERNZAPZUC
      ZEUDNZCPZUFZUNUOVGEABCDFKGHIJUGUHVFVQKELUPEUIZURVIVCVNVEVPVRUQVHFUPEOSTVR
      UTVKVBVMVRUSVJBUPEQSTVRVAVLAUPERSTUJVRVDVOCUPEUDSTUKULUM $.
      $( [17-Sep-2009] $)
  $}

  ${
    $d a x y $.
    $( Lemma to simplify some subcategories related theorems . $)
    besubbeca $p |- ( U e. ( SubCat ` T ) -> T e. Cat ) $=
      ( vx vy va csubc cfv wcel cdm ccat elfvdm cv cid_ wss cdom_ ccod_ co_ w3a
      wa crab wceq copab dmopabss sseli df-subc dmeqi eleq2s syl ) BAFGHAFIZHAJ
      HZBAFKUJACLZJHDLELZMGUKMGNULOGUKOGNULPGUKPGNSULQGUKQGNREJTUAZSCDUBZIZUIUO
      JAUMCDJUCUDFUNCDEUEUFUGUH $.
      $( [17-Sep-2009] $)
  $}

  $( A category belongs to the set of its subcategories.  (Contributed by FL,
     17-Sep-2009.) $)
  catsbc $p |- ( T e. Cat -> T e. ( SubCat ` T ) ) $=
    ( ccat wcel csubc cfv cid_ wss cdom_ ccod_ co_ w3a ssid pm3.2i 3pm3.2i eqid
    wa wb issubcatb anidms mpbiri ) ABCZAADECZAFEZUCGZAHEZUEGZAIEZUGGZPZAJEZUJG
    ZKZUDUIUKUCLUFUHUELUGLMUJLNUAUBULQUGUEUJAAUCUEOUGOUJOUCORST $.
    $( [17-Sep-2009] $)

  ${
    obsubc2.1 $e |- O1 = dom ( id_ ` T ) $.
    obsubc2.2 $e |- O2 = dom ( id_ ` U ) $.
    $( The objects of a subcategory are a subset of those of the
       supercategory.  JFM CAT_2 th. 11 .  (Contributed by FL, 17-Sep-2009.) $)
    obsubc2 $p |- ( U e. ( SubCat ` T ) -> O2 C_ O1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cid_ cdm cdom_ ccod_ co_ w3a eqid
      issubcata dmss 3ad2ant1 adantl syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCK
      ABLUIUJMBNJZOZANJZOZDCUIUJULUNKZUIUJBGHZUKUMKZBPJAPJZKBQJAQJZKMZBRJARJZKZ
      SZMUOUSURVAABUMURTUSTVATUMTUAVCUOUPUQUTUOVBUKUMUBUCUDUEUFFEUGUH $.
      $( [17-Sep-2009] $)
  $}

  ${
    idsubc.1 $e |- I1 = ( id_ ` T ) $.
    idsubc.2 $e |- I2 = ( id_ ` U ) $.
    $( The identity function of a subcategory is a subset of the identity
       function of the supercategory.  (Contributed by FL, 17-Sep-2009.) $)
    idsubc $p |- ( U e. ( SubCat ` T ) -> I2 C_ I1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cid_ cdom_ ccod_ co_ w3a issubcata
      eqid sseq2i biimpi 3ad2ant1 adantl syl6bi imp 3sstr4g mpancom ) AGHZBAIJH
      ZDCKABLUIUJMBNJZANJZDCUIUJUKULKZUIUJBGHZUKCKZBOJAOJZKBPJAPJZKMZBQJAQJZKZR
      ZMUMUQUPUSABCUPTUQTUSTESVAUMUNUOURUMUTUOUMCULUKEUAUBUCUDUEUFFEUGUH $.
      $( [17-Sep-2009] $)
  $}

  ${
    domsubc.1 $e |- D1 = ( dom_ ` T ) $.
    domsubc.2 $e |- D2 = ( dom_ ` U ) $.
    $( The domain function of a subcategory is a subset of the domain function
       of the supercategory.  (Contributed by FL, 19-Sep-2009.) $)
    domsubc $p |- ( U e. ( SubCat ` T ) -> D2 C_ D1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa cdom_ cid_ ccod_ co_ w3a issubcata
      eqid simpr2l syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZANJZDC
      UFUGUHUIKZUFUGBGHZBOJAOJZKZUJBPJAPJZKZMBQJAQJZKZRMUJUNUIUPABULUITUNTUPTUL
      TSUJUOUMUQUKUAUBUCFEUDUE $.
      $( [19-Sep-2009] $)
  $}

  ${
    codsubc.1 $e |- C1 = ( cod_ ` T ) $.
    codsubc.2 $e |- C2 = ( cod_ ` U ) $.
    $( The codomain function of a subcategory is a subset of the codomain
       function of the supercategory.  (Contributed by FL, 19-Sep-2009.) $)
    codsubc $p |- ( U e. ( SubCat ` T ) -> C2 C_ C1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa ccod_ cid_ cdom_ co_ w3a issubcata
      eqid simpr2r syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZANJZDC
      UFUGUHUIKZUFUGBGHZBOJAOJZKZBPJAPJZKZUJMBQJAQJZKZRMUJUIUNUPABULUNTUITUPTUL
      TSUOUJUMUQUKUAUBUCFEUDUE $.
      $( [19-Sep-2009] $)
  $}

  $( A subcategory is a category.  (Contributed by FL, 17-Sep-2009.) $)
  subctct $p |- ( U e. ( SubCat ` T ) -> U e. Cat ) $=
    ( ccat wcel csubc cfv besubbeca cid_ wss cdom_ ccod_ co_ w3a eqid issubcata
    wa simpl syl6bi mpcom ) ACDZBAEFDZBCDZABGTUAUBBHFAHFZIBJFAJFZIBKFAKFZIPBLFA
    LFZIMZPUBUEUDUFABUCUDNUENUFNUCNOUBUGQRS $.
    $( [17-Sep-2009] $)

  ${
    morsubc.1 $e |- M1 = dom ( dom_ ` T ) $.
    morsubc.2 $e |- M2 = dom ( dom_ ` U ) $.
    $( The morphisms of a subcategory are a subset of those of the
       supercategory.  (Contributed by FL, 18-Sep-2009.) $)
    morsubc $p |- ( U e. ( SubCat ` T ) -> M2 C_ M1 ) $=
      ( csubc cfv wcel wss ccat cid_ cdom_ ccod_ wa co_ w3a wb eqid cdm 3sstr4g
      besubbeca issubcata syl dmss adantr 3ad2ant2 adantl syl6bi pm2.43i ) BAGH
      IZDCJZUKUKBKIZBLHALHZJZBMHZAMHZJZBNHANHZJZOZBPHAPHZJZQZOZULUKAKIUKVERABUB
      USUQVBABUNUQSUSSVBSUNSUCUDVDULUMVAUOULVCURULUTURUPTUQTDCUPUQUEFEUAUFUGUHU
      IUJ $.
      $( [18-Sep-2009] $)
  $}

  ${
    cmpsubc.1 $e |- Ro1 = ( o_ ` T ) $.
    cmpsubc.2 $e |- Ro2 = ( o_ ` U ) $.
    $( The composition law of a subcategory is a subset of the composition law
       of the supercategory.  (Contributed by FL, 20-Sep-2009.) $)
    cmpsubc $p |- ( U e. ( SubCat ` T ) -> Ro2 C_ Ro1 ) $=
      ( ccat wcel csubc cfv wss besubbeca wa co_ cid_ cdom_ ccod_ w3a issubcata
      eqid simpr3 syl6bi imp 3sstr4g mpancom ) AGHZBAIJHZDCKABLUFUGMBNJZANJZDCU
      FUGUHUIKZUFUGBGHZBOJAOJZKZBPJAPJZKBQJAQJZKMZUJRMUJUOUNUIABULUNTUOTUITULTS
      UKUMUPUJUAUBUCFEUDUE $.
      $( [20-Sep-2009] $)
  $}

  ${
    $d T x $.  $d U x $.
    idsubidsup.1 $e |- I1 = ( id_ ` T ) $.
    idsubidsup.2 $e |- I2 = ( id_ ` U ) $.
    idsubidsup.3 $e |- O2 = dom ( id_ ` U ) $.
    $( The identity of an an objet of the subcategory equals the identity of
       the object in the supercategory.  (Contributed by FL, 2-Nov-2009.) $)
    idsubidsup $p |- ( U e. ( SubCat ` T ) ->
      A. x e. O2 ( I2 ` x ) = ( I1 ` x ) ) $=
      ( csubc cfv wcel cv wceq wa wfun cdm syl eqid adantr wss ccat calg catded
      besubbeca cded dedalg cdom_ ida ffun 3syl idsubc cid_ eqcomi dmeqi eleq2i
      wf eqtri biimpi adantl w3a funssfv eqcomd syl3anc ralrimiva ) CBJKLZAMZFK
      ZVGEKZNZADVFVGDLZOEPZFEUAZVGFQZLZVJVFVLVKVFBUBLZBUCLZVLBCUEVPBUFLVQBUDBUG
      RVQEQZBUHKZQZEUQVLVSBEVTVRVTSVSSVRSGUIVRVTEUJRUKTVFVMVKBCEFGHULTVKVOVFVKV
      ODVNVGDCUMKZQVNIWAFFWAHUNUOURUPUSUTVLVMVOVAVIVHVGEFVBVCVDVE $.
      $( [2-Nov-2009] $)
  $}

  ${
    $d M m n o p $.  $d T m n o p $.  $d U m n o p $.
    idsubfun.1 $e |- M = dom ( dom_ ` U ) $.
    $( The identity restricted to the morphism of a subcategory ` U ` is a
       functor from the subcategory to the supercategory.  It is called the
       inclusion functor.  JFM CAT_2 th. 19.  (Contributed by FL,
       5-Oct-2009.) $)
    idsubfun $p |- ( U e. ( SubCat ` T ) ->
      ( _I |` M ) e. ( Func ` <. U , T >. ) ) $=
      ( vm cfv wcel cid cvv wa cdm syl wceq wss eqid adantl fvi fvres ad2antrr
      wi vo vp vn csubc ccat cres cfunc besubbeca subctct cdom_ fvex dmexg mp1i
      cop syl5eqel resiexg w3a wf cv cid_ wrex wral ccod_ co_ co wf1o f1oi f1of
      morsubc fss sylancr obsubc2 ssel jdmo ex a1dd 3ad2ant1 3imp wfun idc ffun
      ax-mp 3ad2ant2 idsubc 3ad2ant3 simp2 funssfv syl3anc 3eqtr4a fveq2 eqeq2d
      rcla4ev syl5com 3exp com14 syl6 com13 pm2.43i com4l mpcom impcom ralrimiv
      simpll1 simpl1 eleq2i biimpi syl2an syl2anc eqtrd ad2antlr domsubc eqcomd
      domc fveq2d eqtr3d eqtr2d 3eqtrd ralrimiva cdmo sylan codc codsubc morcat
      dmo syl5eq adantr eleq2d biimpa jca com23 imp31 cmpmorp sylc ovex adantll
      id fvresi oveq12 wb mpbird cmppfc1 cmpsubc opeq12d cmppfcd eqeltrd eqtr4d
      oprssopvg exp31 3jca isfunb 3exp1 mpd mp2and ) BAUDFGZAUEGZBUEGZHCUFZBAUN
      UGFGZABUHABUIUUNUUQIGZUUOUUPJZUURTUUNCIGUUSUUNCBUJFZKZIDUVAIGUVBIGUUNBUJU
      KUVAIULUMUOCIUPLUUTUUSUUNUURUUPUUOUUSUUNUURTTUUPUUOUUSUUNUURUUPUUOUUSUQZU
      UNJZUURCAUJFZKZUUQURZUAUSZBUTFZFZUUQFZUBUSZAUTFZFZMZUBUVMKZVAZUAUVIKZVBZE
      USZUVAFZUVIFZUUQFZUVTUUQFZUVEFZUVMFZMZECVBZUVTBVCFZFZUVIFZUUQFZUWDAVCFZFZ
      UVMFZMZECVBZJZUCUSZUWIFZUWAMZUVTUWSBVDFZVEZUUQFZUWDUWSUUQFZAVDFZVEZMZTZUC
      CVBZECVBZUQZJZUVDUVGUXLUVDCCUUQURZCUVFNZUVGCCUUQVFUXNCVGCCUUQVHWBUUNUXOUV
      CABUVFCUVFOZDVIPCCUVFUUQVJVKUVDUVSUWRUXKUVDUVQUAUVRUUNUVCUVHUVRGZUVQTZUVR
      UVPNZUUNUVCUXRTABUVPUVRUVPOZUVROZVLUXQUXSUUNUVCUVQUXQUXSUUNUVCUVQTTZTUXSU
      XQUXQUYBUXSUXQUVHUVPGZUXQUYBTUVRUVPUVHVMUVCUXQUUNUYCUVQUVCUXQUUNUYCUVQTUV
      CUXQUUNUQZUVKUVHUVMFZMZUYCUVQUYDUVJHFZUVJUVKUYEUVJIGUYGUVJMUVHUVIUKUVJIQW
      BUYDUVJCGZUVKUYGMUVCUXQUUNUYHUUPUUOUXQUUNUYHTTUUSUUPUXQUYHUUNUUPUXQUYHUVH
      BUVICUVRDUYAUVIOZVNVOVPVQVRUVJCHRLUYDUVMVSZUVIUVMNZUXQUYEUVJMUVCUXQUYJUUN
      UUOUUPUYJUUSUUOUVPUVFUVMURUYJUVEAUVMUVFUVPUXPUVEOZUXTUVMOZVTUVPUVFUVMWALW
      CZVQUUNUVCUYKUXQABUVMUVIUYMUYIWDZWEUVCUXQUUNWFUVHUVMUVIWGWHWIUYCUYFUVQUVO
      UYFUBUVHUVPUVLUVHMUVNUYEUVKUVLUVHUVMWJWKWLVOWMWNWOWPWQWRWSWTXAXBUVDUWHUWQ
      UVDUWGECUVDUVTCGZJZUWCUWBUVTUVEFZUVMFZUWFUYQUWCUWBHFZUWBUYQUWBCGZUWCUYTMU
      YQUUPUWAUVRGZVUAUUPUUOUUSUUNUYPXCZUVDUUPUVTUVBGZVUBUYPUUPUUOUUSUUNXDZUYPV
      UDCUVBUVTDXEXFZUVABUVTUVBUVRUVBOUYAUVAOZYDXGZUWABUVICUVRDUYAUYIVNXHUWBCHR
      LUWBIGUYTUWBMUYQUWAUVIUKUWBIQUMXIUYQUWAUVMFZUWBUYSUYQUYJUYKVUBVUIUWBMUVCU
      YJUUNUYPUYNSZUUNUYKUVCUYPUYOXJZVUHUWAUVMUVIWGWHUYQUWAUYRUVMUYQUYRUWAUYQUV
      EVSZUVAUVENZVUDUYRUWAMUVCVULUUNUYPUUOUUPVULUUSUUOUVFUVPUVEURVULUVEAUVMUVF
      UVPUXPUYLUXTUYMXMUVFUVPUVEWALWCSUUNVUMUVCUYPABUVEUVAUYLVUGXKXJUYPVUDUVDVU
      FPUVTUVEUVAWGWHXLXNXOUYQUYRUWEUVMUYQUVTUWDUVEUYQUWDUVTHFZUVTUYPUWDVUNMUVD
      UVTCHRPUYPVUNUVTMUVDUVTCQPXPZXNXNXQXRUVDUWPECUYQUWLUWKUVTUWMFZUVMFZUWOUYQ
      UWLUWKHFZUWKUYQUWKCGZUWLVURMUYQUUPUWJUVRGZVUSVUCUVDUUPUYPVUTVUEUWIBUVTCUV
      RDUYAUWIOZXSXTZUWJBUVICUVRDUYAUYIVNXHUWKCHRLUWKIGVURUWKMUYQUWJUVIUKUWKIQU
      MXIUYQUWKUWJUVMFZVUQUYQUYJUYKVUTUWKVVCMVUJVUKVVBUYJUYKVUTUQVVCUWKUWJUVMUV
      IWGXLWHUYQUWJVUPUVMUYQVUPUWJUYQUWMVSZUWIUWMNZUVTUWIKZGZVUPUWJMUVCVVDUUNUY
      PUUOUUPVVDUUSUUOUVFUVPUWMURVVDUWMUVEAUVMUVFUVPUXPUYLUXTUYMUWMOZYAUVFUVPUW
      MWALWCSUUNVVEUVCUYPABUWMUWIVVHVVAYBXJUVDUYPVVGUVDCVVFUVTUVCCVVFMZUUNUUPUU
      OVVIUUSUUPCUVBVVFDBYCYEVQYFYGYHUVTUWMUWIWGWHXLXNXIUYQVUPUWNUVMUYQUVTUWDUW
      MVUOXNXNXQXRYIUVDUXJECUYQUXIUCCUYQUWSCGZUXAUXHUYQVVJJZUXAJZUXDUXCHFZUXCUX
      GVVLUXCCGZUXDVVMMVVLUUPVVJUYPUQZUWAUWTMZVVNVVKVVOUXAUVDUYPVVJVVOUVCUYPVVJ
      VVOTTZUUNUUPUUOVVQUUSUUPVVJUYPVVOUUPVVJUYPVVOVVOYPWNYJVQYFYKYFZUXAVVPVVKU
      XAUWTUWAUXAYPXLPZUWIUVAUXBBUWSUVTCDVUGVVAUXBOZYLYMUXCCHRLUXCIGVVMUXCMVVLU
      VTUWSUXBYNUXCIQUMVVLUXCUWDUXEUXBVEZUXGVVLUVTUWDMZUWSUXEMZJZUXCVWAMVVKVWDU
      XAUYPVVJVWDUVDUYPVVJJZVWBVWCVWEUWDUVTUYPUWDUVTMZVVJCUVTYQZYFXLVWEUXEUWSHF
      ZUWSVVJUXEVWHMUYPUWSCHRPVVJVWHUWSMUYPUWSCQPXPYIYOYFUVTUWDUWSUXEUXBYRLVVLU
      XFVSZUXBUXFNZUWDUXEUNZUXBKZGUXGVWAMUYQVWIVVJUXAUVCVWIUUNUYPUUOUUPVWIUUSUX
      FAUXFOZUUAWCSSUYQVWJVVJUXAUUNVWJUVCUYPABUXFUXBVWMVVTUUBXJSVVLVWKUVTUWSUNZ
      VWLVVLUWDUVTUXEUWSUYQVWFVVJUXAUYPVWFUVDVWGPSVVJUXEUWSMUYQUXACUWSYQXJUUCVV
      LVWNVWLGZVVPVVSVVLVVOVWOVVPYSVVRUWIUVAUXBBUWSUVTCDVUGVVAVVTUUDLYTUUEUWDUX
      EUXFUXBUUGWHUUFXQUUHXBXRUUIYIUVCUURUXMYSUUNIBAEUCUAUUQUBUVRCUVPUVFUVIUVAU
      WIUXBUVMUVEUWMUXFUYADVUGVVAUYIVVTUXTUXPUYLVVHUYMVWMUUJYFYTUUKXAWQUULUUM
      $.
      $( [5-Oct-2009] $)
  $}

  ${
    $d M1 x $.  $d M2 x $.
    infemb.1 $e |- M1 = dom ( dom_ ` T ) $.
    infemb.2 $e |- M2 = dom ( dom_ ` U ) $.
    $( The inclusion functor is an embedding.  (Contributed by FL,
       2-Nov-2009.) $)
    infemb $p |- ( U e. ( SubCat ` T ) -> ( _I |` M2 ) : M2 -1-1-> M1 ) $=
      ( vx csubc cfv wcel cv cid cres wf1o wss wa wex wf1 cdom_ cvv morsubc cdm
      f1oi fvex dmex eqeltri f1oeq3 sseq1 anbi12d cla4ev sylancr resiexg sylibr
      wceq ax-mp f11o ) BAHIJZDGKZLDMZNZURCOZPZGQZDCUSRUQDDUSNZDCOZVCDUCABCDEFU
      AVBVDVEPGDDBSIZUBTFVFBSUDUEUFZURDUNUTVDVAVEURDDUSUGURDCUHUIUJUKGDCUSDTJUS
      TJVGDTULUOUPUM $.
      $( [2-Nov-2009] $)
  $}

