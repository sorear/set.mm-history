$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           Pocklington's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d k n x D $.  $d k K $.  $d k x N $.  $d k n x P $.
    $( A relation involving divisibility by a prime power.  (Contributed by
       Mario Carneiro, 2-Mar-2014.) $)
    prmpwdvds $p |- ( ( ( K e. ZZ /\ D e. ZZ ) /\ ( P e. Prime /\ N e. NN ) /\
         ( D || ( K x. ( P ^ N ) ) /\ -. D || ( K x. ( P ^ ( N - 1 ) ) ) ) ) ->
                   ( P ^ N ) || D ) $=
      ( vk cz wcel wa cexp co cmul cdivides c1 wn wi wceq oveq2d breq2d syl2anc
      wbr vx vn cprime cn cmin cv wral simpll caddc oveq2 notbid anbi12d breq1d
      oveq1 imbi12d ralbidv imbi2d weq breq1 breq2 cgcd wb simplrl coprm cc zcn
      ad2antll ad2antrl syl mulcom simpl gcdcom eqeq1d simprr coprmdvds syl3anc
      prmz sylbid expdimp expimpd ex vtoclga impl exp1 3syl ad2antlr cc0 ax-1cn
      con1d subidi oveq2i simplr exp0 syl5eq adantl mulid1 eqtrd 3imtr4d imbi1d
      ralrimiva cbvralv zmulcl rcla4v cn0 nnnn0 ad2antrr zexpcl divides adantll
      wrex prmnn nncn expp1 nnexpcl mulass eqtr4d wne nnne0 dvdsmulcr syl112anc
      nnz bitrd an32s syl5ibcom rexlimdva adantlr com23 a2d expm1t pncan anbi2d
      nnm1nn0 sylancl syld anassrs ralrimdva syl5bi expl nnind com12 impr sylc
      3impia ) CFGZAFGZHZBUCGZDUDGZHZACBDIJZKJZLTZACBDMUEJZIJZKJZLTZNZHZUUJALTZ
      UUFUUIHUUDAEUFZUUJKJZLTZAUUTUUNKJZLTZNZHZUUSOZEFUGZUURUUSOZUUDUUEUUIUHUUE
      UUIUVHUUDUUEUUGUUHUVHUUHUUEUUGHZUVHUVJAUUTBUAUFZIJZKJZLTZAUUTBUVKMUEJZIJZ
      KJZLTZNZHZUVLALTZOZEFUGZOUVJAUUTBMIJZKJZLTZAUUTBMMUEJZIJZKJZLTZNZHZUWDALT
      ZOZEFUGZOUVJAUUTBUBUFZIJZKJZLTZAUUTBUWPMUEJZIJZKJZLTZNZHZUWQALTZOZEFUGZOU
      VJAUUTBUWPMUIJZIJZKJZLTZAUUTBUXIMUEJZIJZKJZLTZNZHZUXJALTZOZEFUGZOUVJUVHOU
      AUBDUVKMPZUWCUWOUVJUYBUWBUWNEFUYBUVTUWLUWAUWMUYBUVNUWFUVSUWKUYBUVMUWEALUY
      BUVLUWDUUTKUVKMBIUJZQRUYBUVRUWJUYBUVQUWIALUYBUVPUWHUUTKUYBUVOUWGBIUVKMMUE
      UNQQRUKULUYBUVLUWDALUYCUMUOUPUQUAUBURZUWCUXHUVJUYDUWBUXGEFUYDUVTUXEUWAUXF
      UYDUVNUWSUVSUXDUYDUVMUWRALUYDUVLUWQUUTKUVKUWPBIUJZQRUYDUVRUXCUYDUVQUXBALU
      YDUVPUXAUUTKUYDUVOUWTBIUVKUWPMUEUNQQRUKULUYDUVLUWQALUYEUMUOUPUQUVKUXIPZUW
      CUYAUVJUYFUWBUXTEFUYFUVTUXRUWAUXSUYFUVNUXLUVSUXQUYFUVMUXKALUYFUVLUXJUUTKU
      VKUXIBIUJZQRUYFUVRUXPUYFUVQUXOALUYFUVPUXNUUTKUYFUVOUXMBIUVKUXIMUEUNQQRUKU
      LUYFUVLUXJALUYGUMUOUPUQUVKDPZUWCUVHUVJUYHUWBUVGEFUYHUVTUVFUWAUUSUYHUVNUVB
      UVSUVEUYHUVMUVAALUYHUVLUUJUUTKUVKDBIUJZQRUYHUVRUVDUYHUVQUVCALUYHUVPUUNUUT
      KUYHUVOUUMBIUVKDMUEUNQQRUKULUYHUVLUUJALUYIUMUOUPUQUVJUWNEFUVJUUTFGZHZAUUT
      BKJZLTZAUUTLTZNZHZBALTZUWLUWMUUEUUGUYJUYPUYQOZUUGUYJHZUVKUYLLTZUVKUUTLTZN
      ZHZBUVKLTZOZOUYSUYROUAAFUVKAPZVUEUYRUYSVUFVUCUYPVUDUYQVUFUYTUYMVUBUYOUVKA
      UYLLUSVUFVUAUYNUVKAUUTLUSUKULUVKABLUTUOUQUVKFGZUYSVUEVUGUYSHZUYTVUBVUDVUH
      UYTHZVUDVUAVUIVUDNZBUVKVAJZMPZVUAVUIUUGVUGVUJVULVBVUGUUGUYJUYTVCVUGUYSUYT
      UHBUVKVDSVUHUYTVULVUAVUHUYTVULHUVKBUUTKJZLTZUVKBVAJZMPZHZVUAVUHUYTVUNVULV
      UPVUHUYLVUMUVKLVUHUUTVEGZBVEGZUYLVUMPUYJVURVUGUUGUUTVFZVGVUHBFGZVUSUUGVVA
      VUGUYJBVQZVHZBVFZVIUUTBVJSRVUHVUKVUOMVUHVVAVUGVUKVUOPVVCVUGUYSVKZBUVKVLSV
      MULVUHVUGVVAUYJVUQVUAOVVEVVCVUGUUGUYJVNUVKBUUTVOVPVRVSVRWIVTZWAWBWCUYKUWF
      UYMUWKUYOUYKUWEUYLALUYKUWDBUUTKUUGUWDBPZUUEUYJUUGVVAVUSVVGVVBVVDBWDZWEWFQ
      RUYKUWJUYNUYKUWIUUTALUYKUWIUUTMKJZUUTUYKUWHMUUTKUYKUWHBWGIJZMUWGWGBIMWHWJ
      WKUYKVUSVVJMPUYKUUGVVAVUSUUEUUGUYJWLVVBVVDWEZBWMVIWNQUYKVURVVIUUTPUYJVURU
      VJVUTWOUUTWPVIWQRUKULUYKUWDBALUYKVUSVVGVVKVVHVIUMWRWTUWPUDGZUVJUXHUYAVVLU
      UEUUGUXHUYAOUXHAUVKUWQKJZLTZAUVKUXAKJZLTZNZHZUXFOZUAFUGZVVLUUEHZUUGHZUYAU
      XGVVSEUAFEUAURZUXEVVRUXFVWCUWSVVNUXDVVQVWCUWRVVMALUUTUVKUWQKUNRVWCUXCVVPV
      WCUXBVVOALUUTUVKUXAKUNRUKULWSXAVWBVVTUXTEFVWAUUGUYJVVTUXTOVWAUYSHZVVTAUYL
      UWQKJZLTZAUYLUXAKJZLTZNZHZUXFOZUXTVWDUYLFGZVVTVWKOVWDUYJVVAVWLVWAUUGUYJVN
      UUGVVAVWAUYJVVBVHZUUTBXBZSVVSVWKUAUYLFUVKUYLPZVVRVWJUXFVWOVVNVWFVVQVWIVWO
      VVMVWEALUVKUYLUWQKUNRVWOVVPVWHVWOVVOVWGALUVKUYLUXAKUNRUKULWSXCVIVWDUXLUWS
      NZHZUXFOVWQUXSOZVWKUXTVWDVWQUXFUXSVWDUXFVWQUXSVWDUXFVVMAPZUAFXJZVWRVWDUWQ
      FGZUUEUXFVWTVBVWDVVAUWPXDGZVXAVWMVVLVXBUUEUYSUWPXEZXFZBUWPXGSZVVLUUEUYSWL
      UAUWQAXHSVVLUYSVWTVWROUUEVVLUYSHZVWSVWRUAFVXFVUGHVVMUXKLTZVVMUWRLTZNZHZUX
      JVVMLTZOZVWSVWRVVLVUGUYSVXLVVLVUGHZUYSHZVUCVUDVXJVXKVUGUYSVUEVVLVVFXIVXNV
      XGUYTVXIVUBVXNVXGVVMVWELTZUYTVXNUXKVWEVVMLVXNUXKUUTBUWQKJZKJZVWEVXNUXJVXP
      UUTKVXNUXJUWQBKJZVXPVXNVUSVXBUXJVXRPZVXNBUDGZVUSUUGVXTVXMUYJBXKVHZBXLVIZV
      VLVXBVUGUYSVXCXFZBUWPXMZSVXNUWQVEGZVUSVXRVXPPVXNUWQUDGZVYEVXNVXTVXBVYFVYA
      VYCBUWPXNSZUWQXLVIZVYBUWQBVJSWQZQVXNVURVUSVYEVWEVXQPZUYJVURVXMUUGVUTVGVYB
      VYHUUTBUWQXOZVPXPRVXNVUGVWLVXAUWQWGXQZVXOUYTVBVVLVUGUYSWLZVXNUYJVVAVWLVXM
      UUGUYJVNZVXNVXTVVAVYABYAVIZVWNSVXNVYFVXAVYGUWQYAVIZVXNVYFVYLVYGUWQXRVIZUW
      QUVKUYLXSXTYBVXNVXHVUAVXNVUGUYJVXAVYLVXHVUAVBVYMVYNVYPVYQUWQUVKUUTXSXTUKU
      LVXNVXKVXPVVMLTZVUDVXNUXJVXPVVMLVYIUMVXNVVAVUGVXAVYLVYRVUDVBVYOVYMVYPVYQU
      WQBUVKXSXTYBWRYCVWSVXJVWQVXKUXSVWSVXGUXLVXIVWPVVMAUXKLUSVWSVXHUWSVVMAUWRL
      USUKULVVMAUXJLUTUOYDYEYFVRYGYHVWDVWJVWQUXFVWDVWFUXLVWIVWPVWDVWEUXKALVWDVW
      EVXQUXKVWDVURVUSVYEVYJUYJVURVWAUUGVUTVGZVWDVVAVUSVWMVVDVIZVWDVXAVYEVXEUWQ
      VFVIZVYKVPVWDVXPUXJUUTKVWDVXPVXRUXJVWDVUSVYEVXPVXRPVYTWUABUWQVJSVWDVUSVXB
      VXSVYTVXDVYDSXPQWQRVWDVWHUWSVWDVWGUWRALVWDVWGUUTBUXAKJZKJZUWRVWDVURVUSUXA
      VEGZVWGWUCPVYSVYTVWDUXAFGZWUDVWDVVAUWTXDGZWUEVWMVVLWUFUUEUYSUWPYLXFBUWTXG
      SUXAVFVIZUUTBUXAXOVPVWDWUBUWQUUTKVWDWUBUXABKJZUWQVWDVUSWUDWUBWUHPVYTWUGBU
      XAVJSVWDVUSVVLUWQWUHPVYTVVLUUEUYSUHBUWPYISXPQWQRUKULWSVWDUXRVWQUXSVWDUXQV
      WPUXLVWDUXPUWSVWDUXOUWRALVWDUXNUWQUUTKVWDUXMUWPBIVWDUWPVEGZMVEGUXMUWPPVVL
      WUIUUEUYSUWPXLXFWHUWPMYJYMQQRUKYKWSWRYNYOYPYQYRYHYSYTUUAXIUVGUVIECFUUTCPZ
      UVFUURUUSWUJUVBUULUVEUUQWUJUVAUUKALUUTCUUJKUNRWUJUVDUUPWUJUVCUUOALUUTCUUN
      KUNRUKULWSXCUUBUUC $.
      $( [2-Mar-2014] $)
  $}

  ${
    pockthg.1 $e |- ( ph -> A e. NN ) $.
    pockthg.2 $e |- ( ph -> B e. NN ) $.
    pockthg.3 $e |- ( ph -> B < A ) $.
    pockthg.4 $e |- ( ph -> N = ( ( A x. B ) + 1 ) ) $.
    ${
      pockthlem.5 $e |- ( ph -> P e. Prime ) $.
      pockthlem.6 $e |- ( ph -> P || N ) $.
      pockthlem.7 $e |- ( ph -> Q e. Prime ) $.
      pockthlem.8 $e |- ( ph -> ( Q pCnt A ) e. NN ) $.
      pockthlem.9 $e |- ( ph -> C e. ZZ ) $.
      pockthlem.10 $e |- ( ph -> ( ( C ^ ( N - 1 ) ) mod N ) = 1 ) $.
      pockthlem.11 $e |- ( ph ->
                          ( ( ( C ^ ( ( N - 1 ) / Q ) ) - 1 ) gcd N ) = 1 ) $.
      $( Lemma for ~ pockthg . $)
      pockthlem $p |- ( ph -> ( Q pCnt A ) <_ ( Q pCnt ( P - 1 ) ) ) $=
        ( c1 wcel cpc co cmin cle wbr cexp cdivides codz cdiv cz cprime cn cmul
        cfv wn pcdvds syl2anc nnz syl dvdsmul1 caddc oveq1d wceq nnmulcl ax-1cn
        cc nncn pncan sylancl eqtrd breqtrrd wa cn0 prmnn pccl nnexpcl cuz nnuz
        wi 1z syl6eleq eluzp1p1 eqeltrd sylancr syl6eleqr dvdstr syl3anc mp2and
        eluzp1m1 cc0 wne wb nnne0 divides2 mpbid cgcd gcddvds simpld gcdcl nn0z
        simprd clt c2 df-2 fveq2i eluz2b2 sylib necon3ai dvdslegcd syl31anc cmo
        simpr nnnn0 zexpcl modgcd gcdcom 3eqtr3d rpexp breqtrd gcdn0cl syl21anc
        gcd1 nnle1eq1 odzcl nnre 1mod eqtr4d a1i moddvds peano2zm odzdvds recnd
        cr divcan1 nprmdvds1 iddvdsexp nn0ge0 nngt0 ge0div breq2d elnn0z mpan2d
        sylanbrc dvdsgcd oveq2d nndivre divass syl112anc 3eqtr2d bitr4d 3imtr3d
        expm1 mtod prmpwdvds syl222anc cphi odzphi phiprm prmuz2 pcdvdsb mpbird
        ) AFBUAUBZFESUCUBZUAUBUDUEZFUVBUFUBZUVCUGUEZAUVEDEUHUNUNZUGUEZUVGUVCUGU
        EZUVFAGSUCUBZUVEUIUBZUJTZUVGUJTZFUKTZUVBULTZUVGUVKUVEUMUBZUGUEUVGUVKFUV
        BSUCUBUFUBZUMUBZUGUEZUOUVHAUVEUVJUGUEZUVLAUVEBUGUEZBUVJUGUEZUVTAUVNBULT
        ZUWANHFBUPUQABBCUMUBZUVJUGABUJTZCUJTZBUWDUGUEAUWCUWEHBURUSZACULTZUWFICU
        RUSBCUTUQAUVJUWDSVAUBZSUCUBZUWDAGUWISUCKVBAUWDVFTZSVFTUWJUWDVCAUWDULTZU
        WKAUWCUWHUWLHIBCVDUQZUWDVGUSVEUWDSVHVIVJVKAUVEUJTZUWEUVJUJTZUWAUWBVLUVT
        VSAUVEULTZUWNAFULTZUVBVMTZUWPAUVNUWQNFVNUSZAUVNUWCUWRNHFBVOUQZFUVBVPUQZ
        UVEURUSZUWGAUVJULTZUWOAUVJSVQUNZULASUJTZGSSVAUBZVQUNZTUVJUXDTVTAGUWIUXG
        KAUWDUXDTUWIUXGTAUWDULUXDUWMVRWASUWDWBUSWCZSGWIWDVRWEZUVJURUSZUVEBUVJWF
        WGWHZAUWNUVEWJWKZUWOUVTUVLWLUXBAUWPUXLUXAUVEWMUSZUXJUVEUVJWNWGWOAUVGULT
        ZUVMAEULTZDUJTZDEWPUBZSVCZUXNAEUKTZUXOLEVNUSZPAUXQSUDUEZUXRAUXQDGWPUBZS
        UDAUXQDUGUEZUXQGUGUEZUXQUYBUDUEZAUYCUXQEUGUEZAUXPEUJTZUYCUYFVLPAUXOUYGU
        XTEURUSZDEWQUQZWRAUYFEGUGUEZUYDAUYCUYFUYIXAMAUXQUJTZUYGGUJTZUYFUYJVLUYD
        VSAUXQVMTZUYKAUXPUYGUYMPUYHDEWSUQUXQWTUSZUYHAGULTZUYLAUYOSGXBUEZAGXCVQU
        NZTUYOUYPVLAGUXGUYQUXHXCUXFVQXDXEZWEGXFXGZWRZGURUSZUXQEGWFWGWHAUYKUXPUY
        LDWJVCZGWJVCZVLZUOZUYCUYDVLUYEVSUYNPVUAAGWJWKZVUEAUYOVUFUYTGWMUSVUDGWJV
        UBVUCXLXHUSUXQDGXIXJWHADUVJUFUBZGWPUBZSVCZUYBSVCZAVUGGXKUBZGWPUBZSGWPUB
        ZVUHSAVUKSGWPQVBAVUGUJTZUYOVULVUHVCAUXPUVJVMTZVUNPAUXCVUOUXIUVJXMUSZDUV
        JXNUQZUYTVUGGXOUQAVUMGSWPUBZSAUXEUYLVUMVURVCVTVUASGXPWDAUYLVURSVCVUAGYB
        USVJXQAUXPUYLUXCVUIVUJWLPVUAUXIDGUVJXRWGWOXSAUXQULTZUYAUXRWLAUXPUYGVUBE
        WJVCZVLZUOZVUSPUYHAEWJWKZVVBAUXOVVCUXTEWMUSVVAEWJVUBVUTXLXHUSDEXTYAUXQY
        CUSWOZDEYDWGUVGURUSZNOAUVGUVJUVPUGAEVUGSUCUBZUGUEZUVGUVJUGUEZAUYJGVVFUG
        UEZVVGMAVUKSGXKUBZVCZVVIAVUKSVVJQAGYMTZUYPVVJSVCAUYOVVLUYTGYEUSAUYOUYPU
        YSXAGYFUQYGAUYOVUNUXEVVKVVIWLUYTVUQUXEAVTYHVUGSGYIWGWOAUYGUYLVVFUJTZUYJ
        VVIVLVVGVSUYHVUAAVUNVVMVUQVUGYJUSEGVVFWFWGWHAUXOUXPUXRVUOVVGVVHWLUXTPVV
        DVUPDUVJEYKXJWOAUVJVFTUVEVFTZUXLUVPUVJVCAUVJAUXCUVJYMTZUXIUVJYEUSZYLAUW
        PVVNUXAUVEVGUSZUXMUVJUVEYNWGZVKAUVSESUGUEZAUXSVVSUOLEYOUSAEDUVJFUIUBZUF
        UBZSUCUBZUGUEZEVWBGWPUBZUGUEZUVSVVSAVWCUYJVWEMAUYGVWBUJTZUYLVWCUYJVLVWE
        VSUYHAVWAUJTZVWFAUXPVVTVMTZVWGPAVVTUJTZWJVVTUDUEZVWHAFUVJUGUEZVWIAFUVEU
        GUEZUVTVWKAFUJTZUVOVWLAUWQVWMUWSFURUSZOFUVBYPUQUXKAVWMUWNUWOVWLUVTVLVWK
        VSVWNUXBUXJFUVEUVJWFWGWHAVWMFWJWKZUWOVWKVWIWLVWNAUWQVWOUWSFWMUSZUXJFUVJ
        WNWGWOAWJUVJUDUEZVWJAVUOVWQVUPUVJYQUSAVVOFYMTZWJFXBUEZVWQVWJWLVVPAUWQVW
        RUWSFYEUSZAUWQVWSUWSFYRUSUVJFYSWGWOVVTUUAUUCZDVVTXNUQVWAYJUSVUAEVWBGUUD
        WGUUBAVWCUVGVVTUGUEZUVSAUXOUXPUXRVWHVWCVXBWLUXTPVVDVXADVVTEYKXJAUVRVVTU
        VGUGAUVRUVKUVEFUIUBZUMUBZUVPFUIUBZVVTAUVQVXCUVKUMAFVFTZVWOUVBUJTZUVQVXC
        VCAFVWTYLZVWPAUVOVXGOUVBURUSFUVBUULWGUUEAUVKVFTVVNVXFVWOVXEVXDVCAUVKAVV
        OUWPUVKYMTVVPUXAUVJUVEUUFUQYLVVQVXHVWPUVKUVEFUUGUUHAUVPUVJFUIVVRVBUUIYT
        UUJAVWDSEUGRYTUUKUUMUVGFUVKUVBUUNUUOAUVGEUUPUNZUVCUGAUXOUXPUXRUVGVXIUGU
        EUXTPVVDDEUUQWGAUXSVXIUVCVCLEUURUSXSAUWNUVMUVCUJTZUVHUVIVLUVFVSUXBVVEAU
        VCULTZVXJAUVCUXDULAUXEEUXGTUVCUXDTVTAEUYQUXGAUXSEUYQTLEUUSUSUYRWASEWIWD
        VRWEZUVCURUSUVEUVGUVCWFWGWHAUVNVXKUWRUVDUVFWLNVXLUWTUVBFUVCUUTWGUVA $.
        $( [2-Mar-2014] $)
    $}

    $(
    pockthg @p |- ( ph -> ( N e. Prime <-> A. p e. Prime ( p || A -> E. x e. ZZ
                   ( ( ( x ^ ( N - 1 ) ) mod N ) = 1 /\
                     ( ( ( x ^ ( ( N - 1 ) / p ) ) - 1 ) gcd N ) = 1 ) ) ) ) @=
      ? @.
    $)

    $d p q x N $.  $d p x A $.  $d p q x ph $.
    pockthg.5 $e |- ( ph -> A. p e. Prime ( p || A -> E. x e. ZZ
                   ( ( ( x ^ ( N - 1 ) ) mod N ) = 1 /\
                     ( ( ( x ^ ( ( N - 1 ) / p ) ) - 1 ) gcd N ) = 1 ) ) ) $.
    $( The generalized Pocklington's theorem.  If ` N - 1 = A x. B ` where
       ` B < A ` , then ` N ` is prime if and only if for every prime factor
       ` p ` of ` A ` , there is an ` x ` such that
       ` x ^ ( N - 1 ) = 1 ( mod N ) ` and
       ` gcd ( x ^ ( ( N - 1 ) / p ) - 1 , N ) = 1 ` .  (Contributed by Mario
       Carneiro, 3-Mar-2014.) $)
    pockthg $p |- ( ph -> N e. Prime ) $=
      ( wcel co cle wbr c1 cn syl wa adantr vq c2 cuz cfv cv cdivides wn cprime
      cexp wi wral cmul nnmulcl syl2anc nnuz syl6eleq eluzp1p1 fveq2i syl6eleqr
      caddc df-2 eqeltrd clt cr eluzelre nnre resqcl 3syl prmnn ad2antrl cc0 wb
      nngt0 ltmul2 syl112anc mpbid nnltp1le cc wceq recnd sqval 3brtr4d cpc cmo
      cmin cdiv cgcd cz wrex wo simpr ad2antrr pccl elnn0 sylib nncn exp1 nnge1
      cn0 ad2antll simprl 1nn0 a1i pcdvdsb syl3anc eqbrtrrd w3a simpl2l simpl2r
      simpl3l simpl3r simprrl simprrr pockthlem expr rexlimdva 3expa embantd id
      simpl1 prmuz2 uz2m1nn syl2anr breq1 syl5ibrcom a1dd jaod ralimdva pc2dvds
      nn0ge0 mpd mpbird nnz dvdsle nnnn0 nn0ltlem1 lt2sq syl22anc lelttrd ltnle
      con2d ralrimiva isprm5 sylanbrc ) AEUBUCUDZLZUAUEZUBUIMZENOZUUGEUFOZUGUJZ
      UAUHUKEUHLAECDULMZPUTMZUUEJAUUMPPUTMZUCUDZUUEAUULPUCUDZLUUMUUOLAUULQUUPAC
      QLZDQLZUULQLZGHCDUMUNZUOUPPUULUQRUBUUNUCVAURUSVBZAUUKUAUHAUUGUHLZSUUJUUIA
      UVBUUJUUIUGZAUVBUUJSZSZEUUHVCOZUVCUVEECUBUIMZUUHAEVDLZUVDAUUFUVHUVAUBEVER
      TZAUVGVDLZUVDAUUQCVDLZUVJGCVFZCVGVHTUVEUUGQLZUUGVDLZUUHVDLZUVBUVMAUUJUUGV
      IVJZUUGVFZUUGVGVHZAEUVGNOUVDAUUMCCULMZEUVGNAUULUVSVCOZUUMUVSNOZADCVCOZUVT
      IADVDLZUVKUVKVKCVCOZUWBUVTVLAUURUWCHDVFRAUUQUVKGUVLRZUWEAUUQUWDGCVMRDCCVN
      VOVPAUUSUVSQLZUVTUWAVLUUTAUUQUUQUWFGGCCUMUNUULUVSVQUNVPJACVRLUVGUVSVSACUW
      EVTCWARWBTUVECUUGVCOZUVGUUHVCOZUVEUWGCUUGPWEMZNOZUVECUWIUFOZUWJUVEUWKFUEZ
      CWCMZUWLUWIWCMZNOZFUHUKZUVEUWLCUFOZBUEZEPWEMZUIMEWDMPVSZUWRUWSUWLWFMUIMPW
      EMEWGMPVSZSZBWHWIZUJZFUHUKZUWPAUXEUVDKTUVEUXDUWOFUHUVEUWLUHLZSZUWMQLZUWMV
      KVSZWJZUXDUWOUJZUXGUWMWSLZUXJUXGUXFUUQUXLUVEUXFWKAUUQUVDUXFGWLUWLCWMUNUWM
      WNWOUXGUXHUXKUXIUVEUXFUXHUXKUVEUXFUXHSZSZUWQUXCUWOUXNUWLPUIMZUWLCUFUXNUWL
      QLZUWLVRLUXOUWLVSUXFUXPUVEUXHUWLVIVJUWLWPUWLWQVHUXNPUWMNOZUXOCUFOZUXHUXQU
      VEUXFUWMWRWTUXNUXFUUQPWSLZUXQUXRVLUVEUXFUXHXAAUUQUVDUXMGWLUXSUXNXBXCPUWLC
      XDXEVPXFAUVDUXMUXCUWOUJAUVDUXMXGZUXBUWOBWHUXTUWRWHLZUXBUWOUXTUYAUXBSZSZCD
      UWRUUGUWLEUYCAUUQAUVDUXMUYBXTZGRUYCAUURUYDHRUYCAUWBUYDIRUYCAEUUMVSUYDJRUV
      BUUJAUXMUYBXHUVBUUJAUXMUYBXIUXFUXHAUVDUYBXJUXFUXHAUVDUYBXKUXTUYAUXBXAUXTU
      YAUWTUXAXLUXTUYAUWTUXAXMXNXOXPXQXRXOUXGUXIUWOUXDUXGUWOUXIVKUWNNOZUXGUWNWS
      LZUYEUXFUXFUWIQLZUYFUVEUXFXSUVBUYGAUUJUVBUUGUUELUYGUUGYAUUGYBRVJZUWLUWIWM
      YCUWNYJRUWMVKUWNNYDYEYFYGYKYHYKUVEUUQUYGUWKUWPVLAUUQUVDGTUYHCUWIFYIUNYLUV
      ECWHLZUYGUWKUWJUJAUYIUVDAUUQUYIGCYMRTUYHCUWIYNUNYKUVECWSLZUUGWSLZUWGUWJVL
      AUYJUVDAUUQUYJGCYOZRTUVEUVMUYKUVPUUGYOZRCUUGYPUNYLUVEUVKVKCNOZUVNVKUUGNOZ
      UWGUWHVLAUVKUVDUWETAUYNUVDAUUQUYJUYNGUYLCYJVHTUVEUVMUVNUVPUVQRUVEUVMUYKUY
      OUVPUYMUUGYJVHCUUGYQYRVPYSUVEUVHUVOUVFUVCVLUVIUVREUUHYTUNVPXOUUAUUBUAEUUC
      UUD $.
      $( [2-Mar-2014] $)
  $}

  ${
    $d x y D $.  $d x y E $.  $d x y N $.  $d x y P $.  $d y A $.
    pockthi.p $e |- P e. Prime $.
    pockthi.g $e |- G e. NN $.
    pockthi.m $e |- M = ( G x. P ) $.
    pockthi.n $e |- N = ( M + 1 ) $.
    pockthi.d $e |- D e. NN $.
    pockthi.e $e |- E e. NN $.
    pockthi.a $e |- A e. NN $.
    pockthi.fac $e |- M = ( D x. ( P ^ E ) ) $.
    pockthi.gt $e |- D < ( P ^ E ) $.
    pockthi.mod $e |- ( ( A ^ M ) mod N ) = ( 1 mod N ) $.
    pockthi.gcd $e |- ( ( ( A ^ G ) - 1 ) gcd N ) = 1 $.
    $( Pocklington's theorem, which gives a sufficient criterion for a number
       ` N ` to be prime.  This is the preferred method for verifying large
       primes, being much more efficient to compute than trial division.  This
       form has been optimized for application to specific large primes; see
       ~ pockthg for a more general closed-form version.  (Contributed by Mario
       Carneiro, 2-Mar-2014.) $)
    pockthi $p |- N e. Prime $=
      ( co c1 vy vx cn wcel cprime cexp cn0 prmnn ax-mp nnnn0i nnexpcl mp2an id
      a1i clt wbr cmul caddc wceq nncni mulcomi eqtri oveq1i cdivides cmin cdiv
      cv cgcd wa cz wrex wi wral wb prmdvdsexpb mp3an23 eqcomi nnmulcli eqeltri
      cmo peano2nn ax-1cn subadd2i mpbir oveq2i nnrei cc0 nngt0i ltaddpos2 mpbi
      cr breqtrri 1mod 3eqtri oveq2 3eqtrri subcli nnne0i divmuli syl6eq oveq2d
      oveq1d nnzi oveq1 eqeq1d anbi12d rcla4ev mpan sylancr syl6bi rgen pockthg
      1re ) BUCUDZGUEUDLXNUACDUFSZBGUBXOUCUDZXNCUCUDZDUGUDXPCUEUDZXQHCUHUIZDMUJ
      CDUKULZUNXNUMBXOUOUPXNPUNGXOBUQSZTURSZUSXNGFTURSZYBKFYATURFBXOUQSYAOBXOBL
      UTXOXTUTVAVBVCVBUNUBVGZXOVDUPZUAVGZGTVESZUFSZGVTSZTUSZYFYGYDVFSZUFSZTVESZ
      GVHSZTUSZVIZUAVJVKZVLZUBUEVMXNYRUBUEYDUEUDZYEYDCUSZYQYSXRDUCUDYEYTVNHMYDC
      DVOVPYTAYGUFSZGVTSZTUSZAYKUFSZTVESZGVHSZTUSZYQUUBAFUFSZGVTSTGVTSZTUUAUUHG
      VTYGFAUFYGFUSYCGUSGYCKVQGTFGGYCUCKFUCUDYCUCUDFECUQSZUCJECIXSVRVSZFWAUIVSZ
      UTZWBFUUKUTWCWDZWEVCQGWKUDTGUOUPUUITUSGUULWFTYCGUOWGFUOUPZTYCUOUPZFUUKWHF
      WKUDTWKUDUUOUUPVNFUUKWFXMFTWIULWJKWLGWMULWNYTUUFAEUFSZTVESZGVHSTYTUUEUURG
      VHYTUUDUUQTVEYTYKEAUFYTYKYGCVFSZEYDCYGVFWOUUSEUSCEUQSZYGUSYGFUUJUUTUUNJEC
      EIUTZCXSUTZVAWPYGCEGTUUMWBWQUVBUVACXSWRWSWDWTXAXBXBRWTAVJUDUUCUUGVIZYQANX
      CYPUVCUAAVJYFAUSZYJUUCYOUUGUVDYIUUBTUVDYHUUAGVTYFAYGUFXDXBXEUVDYNUUFTUVDY
      MUUEGVHUVDYLUUDTVEYFAYKUFXDXBXBXEXFXGXHXIXJXKUNXLUI $.
      $( [2-Mar-2014] $)
  $}

