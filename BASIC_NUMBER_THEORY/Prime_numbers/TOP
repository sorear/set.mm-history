$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                                Prime numbers
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $c Prime $.

  $( Extend the definition of a class to include the set of prime numbers. $)
  cprime $a class Prime $.

  ${
    $d n p $.
    $( Define the set of prime numbers. $)
    df-prime $a |- Prime = { p e. NN | { n e. NN | n || p } ~~ 2o } $.
  $}

  ${
    $d P n p z $.
    $( The predicate "is a prime number".  A prime number is a positive integer
       with exactly two positive divisors.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    isprm $p |- ( P e. Prime <->
                  ( P e. NN /\ { n e. NN | n || P } ~~ 2o ) ) $=
      ( vp cv cdivides wbr cn crab c2o cen cprime breq2 rabbidv breq1d df-prime
      wceq elrab2 ) BDZCDZEFZBGHZIJFRAEFZBGHZIJFCAGKSAPZUAUCIJUDTUBBGSARELMNBCO
      Q $.
      $( [22-Jun-2011] $)

    $( A prime number is an integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmz $p |- ( P e. Prime -> P e. ZZ ) $=
      ( vn cprime wcel cn cz cv cdivides wbr crab c2o cen isprm simplbi nnz syl
      ) ACDZAEDZAFDQRBGAHIBEJKLIABMNAOP $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( A prime number is a positive integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmnn $p |- ( P e. Prime -> P e. NN ) $=
      ( vz cprime wcel cn cv cdivides wbr crab c2o cen isprm simplbi ) ACDAEDBF
      AGHBEIJKHABLM $.
      $( [22-Jun-2011] $) $( [26-Jun-2011] $)
  $}

  ${
    $d n z $.
    $( 1 is not a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1nprm $p |- 1 e/ Prime $=
      ( vn vz c1 cprime wcel wn cv cdivides wbr cn c2o cen csdm c1o cle sylancl
      wa cz 1nn cr wnel crab csn wceq wi nnz dvdsle nnge1 jctird wb nnre letri3
      1re sylibrd 1z iddvds ax-mp pm3.2i eleq1 breq1 anbi12d mpbiri impbii elsn
      imp elrab 3bitr4i eqriv elexi ensn1 eqbrtri 1sdom2 ensdomtr mp2an sdomnen
      isprm mpbiran mtbir df-nel mpbir ) CDUACDEZFWAAGZCHIZAJUBZKLIZWDKMIZWEFWD
      NLINKMIWFWDCUCZNLBWDWGBGZJEZWHCHIZQZWHCUDZWHWDEWHWGEWKWLWIWJWLWIWJWHCOIZC
      WHOIZQZWLWIWJWMWNWIWHRECJEZWJWMUEWHUFSWHCUGPWHUHUIWIWHTECTEWLWOUJWHUKUMWH
      CULPUNVEWLWKWPCCHIZQWPWQSCREWQUOCUPUQURWLWIWPWJWQWHCJUSWHCCHUTVAVBVCWCWJA
      WHJWBWHCHUTVFBCVDVGVHCCJSVIVJVKVLWDNKVMVNWDKVOUQWAWPWECAVPSVQVRCDVSVT $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d N n $.
    $( The positive divisors of a positive integer include 1 and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1idssfct $p |- ( N e. NN -> { 1 , N } C_ { n e. NN | n || N } ) $=
      ( cn wcel c1 cv cdivides wbr crab cpr wss cz nnz 1dvds 1nn wa breq1 elrab
      biimpri mpan 3syl iddvds syl mpdan prssg ibi syl2anc ) BCDZEAFZBGHZACIZDZ
      BUKDZEBJUKKZUHBLDZEBGHZULBMZBNECDZUPULOULURUPPUJUPAECUIEBGQRSTUAUHBBGHZUM
      UHUOUSUQBUBUCUMUHUSPUJUSABCUIBBGQRSUDULUMPUNEBUKUKUKUEUFUG $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n p $.
    $( Lemma for ~ isprm2 . $)
    isprm2lem $p |- ( ( P e. NN /\ P =/= 1 ) ->
      ( { n e. NN | n || P } ~~ 2o <-> { n e. NN | n || P } = { 1 , P } ) ) $=
      ( vp cn wcel c1 wne cv cdivides wbr crab c2o cen cpr wceq wb wi wa wn csn
      neeq1 breq2 rabbidv breq1d preq2 eqeq12d bibi12d imbi12d wss 1idssfct cun
      cin c0 disjsn c1o 1nn elexi ensn1 vex pm54.43 mp2an notbii 3bitr3ri df-ne
      elsn df-pr breq1i 3bitr4i com 2onn ensym entr sylan2 sylanb cfn prfi nnex
      cvv rabex enfi mpan mpbii adantl wpss dfpss2 pssinf sylanbr an32s ex mt4d
      syl2an eqcomd expr breq1 syl6bbr biimprcd impbid vtoclga imp ) ADEAFGZBHZ
      AIJZBDKZLMJZXCFANZOZPZCHZFGZXAXHIJZBDKZLMJZXKFXHNZOZPZQWTXGQCADXHAOZXIWTX
      OXGXHAFUAXPXLXDXNXFXPXKXCLMXPXJXBBDXHAXAIUBUCZUDXPXKXCXMXEXQXHAFUEUFUGUHX
      HDEZXIXOXRXIRXLXNXRXIXLXNXRXIXLRZRXMXKXRXMXKUIZXMXKMJZXMXKOZXSBXHUJXIXMLM
      JZXLYAXHFOZSZFTZXHTZUKZLMJZXIYCYFYGULUMOZXHYFEZSYIYEYFXHUNYFUOMJYGUOMJYJY
      IPFFDUPUQURXHCUSURYFYGUTVAYKYDCFVEVBVCXHFVDXMYHLMFXHVFVGVHZXLYCLXKMJYAXKL
      LVIVJUQVKXMLXKVLVMVNXTYARZXKVOEZYBYAYNXTYAXMVOEZYNFXHVPXKVREYAYOYNPXJBDVQ
      VSXMXKVRVTWAWBWCYMYBSZYNSZXTYPYAYQXTYPRXMXKWDYAYQXMXKWEXMXKWFWGWHWIWJWKWL
      WMXIXNXLQXRXNXLXIXNXLYCXIXKXMLMWNYLWOWPWCWQWIWRWS $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only positive divisors are 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm2 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. NN ( z || P -> ( z = 1 \/ z = P ) ) ) ) $=
      ( vn cprime wcel cn cv cdivides wbr c1 wss wa wceq wi wn wb 3bitr4i bitri
      ancom wal crab cpr wne c2 cuz cfv wo wral wnel 1nprm df-nel eleq1 biimpcd
      mpbi mtoi df-ne sylibr pm4.71i c2o cen isprm isprm2lem jcab eqss 1idssfct
      imbi2i biantru pm5.74ri adantr bitrd expcom pm5.32d syl5bb pm5.32ri anass
      eluz2b3 bitr4i anbi1i 3bitr2i breq1 elrab elpr imbi12i impexp albii dfss2
      vex df-ral anbi2i 3bitri ) BDEZBFEZCGZBHIZCFUAZJBUBZKZLZBJUCZLZBUDUEUFEZW
      QLZXAAGZBHIZXCJMXCBMUGZNZAFUHZLWKWKWSLWTWKWSWKBJMZOWSWKXHJDEZJDUIXIOUJJDU
      KUNXHWKXIBJDULUMUOBJUPUQURWSWKWRWKWLWOUSUTIZLWSWRBCVAWSWLXJWQWLWSXJWQPWLW
      SLZXJWOWPMZWQBCVBWLXLWQPWSWLXLWQWLWQWPWOKZLZNWLWQNZWLXMNZLWLXLNXOWLWQXMVC
      XLXNWLWOWPVDVFXPXOCBVEVGQVHVIVJVKVLVMVNRWTWSWRLWSWLLZWQLXBWRWSSWSWLWQVOXQ
      XAWQXQXKXAWSWLSBVPVQVRVSWQXGXAXCWOEZXCWPEZNZATXCFEZXFNZATWQXGXTYBAXTYAXDL
      ZXENYBXRYCXSXEWNXDCXCFWMXCBHVTWAXCJBAWGWBWCYAXDXEWDRWEAWOWPWFXFAFWHQWIWJ
      $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)

    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 with no divisors strictly between 1 and itself.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm3 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( 2 ... ( P - 1 ) ) -. z || P ) ) $=
      ( wcel c2 cuz cfv wbr c1 wi cn wa wn co wb cle cz adantr wne cr syl2an cv
      cprime cdivides wceq wral cmin cfz isprm2 iman clt 2nn elnnuz uztrn mpan2
      wo mpbi sylibr nnz dvdsle sylan nnge1 jctild sylan2 zre 1re leltne mp3an1
      nnre 3adant2 3expia anim12d pm4.38 df-ne necom bitri anbi12i ioran bitr4i
      syl6bb syl6 syld imp eluzelz caddc 1z zltp1le mpan breq1i syl6bbr zltlem1
      df-2 anbi12d peano2zm elfz mp3an2 bitr4d bitr3d anasss expcom pm5.32d wss
      2z fzssuz 2re 1lt2 ltleii eluz mp2an mpbir uzss ax-mp sstri nnuz sseqtr4i
      sseli pm4.71ri notbid syl5bb pm5.74da bi2.04 3bitr3g ralbidv2 pm5.32i
      con2b ) BUBCBDEFZCZAUAZBUCGZYGHUDZYGBUDZUOZIZAJUEZKYFYHLZADBHUFMZUGMZUEZK
      ABUHYFYMYQYFYLYNAJYPYFYHYGJCZYKIZIYHYGYPCZLZIYRYLIYTYNIYFYHYSUUAYSYRYKLZK
      ZLYFYHKZUUAYRYKUIUUDUUCYTUUDUUCYRYTKYTUUDYRUUBYTYRUUDUUBYTNZYRYFYHUUEYRYF
      KZYHKHYGUJGZYGBUJGZKZUUBYTUUFYHUUIUUBNZUUFYHHYGOGZYGBOGZKZUUJYFYRBJCZYHUU
      MIYFBHEFZCZUUNYFDUUOCZUUPDJCUUQUKDULUPDBHUMUNBULUQZYRUUNKYHUULUUKYRYGPCZU
      UNYHUULIYGURZYGBUSUTYRUUKUUNYGVAQVBVCYRUUSUUNUUMUUJIYFUUTUURUUSUUNKUUMUUG
      YGHRZNZUUHBYGRZNZKZUUJUUSYGSCZBSCZUUMUVEIUUNYGVDBVHUVFUVGKUUKUVBUULUVDUVF
      UVGUUKUVBUVFUUKUVBUVGHSCUVFUUKUVBVEHYGVFVGVIVJUVFUVGUULUVDYGBVFVJVKTUVEUU
      IUVAUVCKZUUBUUGUUHUVAUVCVLUVHYILZYJLZKUUBUVAUVIUVCUVJYGHVMUVCYGBRUVJBYGVN
      YGBVMVOVPYIYJVQVRVSVTTWAWBUUFUUIYTNZYHYRUUSBPCZUVKYFUUTDBWCUUSUVLKZUUIDYG
      OGZYGYOOGZKZYTUVMUUGUVNUUHUVOUUSUUGUVNNUVLUUSUUGHHWDMZYGOGZUVNHPCZUUSUUGU
      VRNWEHYGWFWGDUVQYGOWKWHWIQYGBWJWLUVLUUSYOPCZYTUVPNZBWMUUSDPCZUVTUWAXBYGDY
      OWNWOVCWPTQWQWRWSWTYTYRYPJYGYPUUOJYPYEUUODYOXCUUQYEUUOXAUUQHDOGZHDVEXDXEX
      FUVSUWBUUQUWCNWEXBHDXGXHXIHDXJXKXLXMXNXOXPWIXQXRXSYHYRYKXTYHYTYDYAYBYCVO
      $.
      $( [27-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  ${
    $d P z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than or equal to 2 whose only divisor greater than or equal to 2 is
       itself.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    isprm4 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                      A. z e. ( ZZ>= ` 2 ) ( z || P -> z = P ) ) ) $=
      ( cprime wcel c2 cuz cfv cv cdivides wbr c1 wceq wo wi wral imbi1i bitr4i
      cn wa imbi2i isprm2 wne eluz2b3 impexp bi2.04 wn df-ne df-or bitri 3bitri
      ralbii2 anbi2i ) BCDBEFGZDZAHZBIJZUOKLZUOBLZMZNZAROZSUNUPURNZAUMOZSABUAVC
      VAUNVBUTAUMRUOUMDZVBNUORDZUOKUBZSZVBNVEVFVBNZNVEUTNVDVGVBUOUCPVEVFVBUDVHU
      TVEVHUPVFURNZNUTVFUPURUEVIUSUPVIUQUFZURNUSVFVJURUOKUGPUQURUHQTUITUJUKULQ
      $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d M m $.  $d P m $.
    $( If ` M ` divides a prime, then ` M ` is either the prime or one.
       (Contributed by Scott Fenton, 8-Apr-2014.) $)
    dvdsprime $p |- ( ( P e. Prime /\ M e. NN ) ->
       ( M || P <-> ( M = P \/ M = 1 ) ) ) $=
      ( vm cprime wcel cn wa cdivides wbr wceq c1 wo c2 cuz wi breq1 syl adantr
      eqeq1 syl5ibrcom cfv cv wral isprm2 orbi12d orcom syl6bb imbi12d rcla4cva
      adantll sylanb cz prmz iddvds 1dvds jaod impbid ) ADEZBFEZGZBAHIZBAJZBKJZ
      LZURAMNUAEZCUBZAHIZVFKJZVFAJZLZOZCFUCZGUSVAVDOZCAUDVLUSVMVEVKVMCBFVFBJZVG
      VAVJVDVFBAHPVNVJVCVBLVDVNVHVCVIVBVFBKSVFBASUEVCVBUFUGUHUIUJUKUTVBVAVCUTVA
      VBAAHIZURVOUSURAULEZVOAUMZAUNQRBAAHPTUTVAVCKAHIZURVRUSURVPVRVQAUOQRBKAHPT
      UPUQ $.
      $( [19-Apr-2014] $) $( [8-Apr-2014] $)
  $}

  ${
    $d x A $.  $d x N $.
    nprmi.1 $e |- A e. NN $.
    nprmi.2 $e |- B e. NN $.
    nprmi.3 $e |- 1 < A $.
    nprmi.4 $e |- 1 < B $.
    nprmi.5 $e |- N = ( A x. B ) $.
    $( An inference for compositeness.  (Contributed by Mario Carneiro,
       18-Feb-2014.) $)
    nprmi $p |- -. N e. Prime $=
      ( vx cdivides wbr wceq wcel wn cn c1 clt cz nnz nnrei cv wi c2 cuz cprime
      cfv wral wrex wa eluz2b2 mpbir2an cmul nnmulcli eqeltri w3a nncni mulcomi
      co eqtr4i dvds0lem mpan2 syl3an mp3an wne cr cc0 wb nngt0i ltmulgt11 mpbi
      ltneii necomi df-ne pm3.2i breq1 eqeq1 imbi12d notbid annim syl6bbr mp2an
      breqtrri rcla4ev rexnal mulgt1 mp4an biantrur isprm4 bitr4i mtbi ) IUAZCJ
      KZWKCLZUBZIUCUDUFZUGZCUEMZWNNZIWOUHZWPNAWOMZACJKZACLZNZUIZWSWTAOMZPAQKZAU
      JDFUKXAXCBOMZXECOMZXAEDCABULURZOHABDEUMZUNZXGBRMZXEARMZXHCRMZXABSASCSXLXM
      XNUOBAULURZCLXAXOXICBABEUPADUPUQHUSBACUTVAVBVCACVDXCCAACADTZCXKTAXICQPBQK
      ZAXIQKZGAVEMZBVEMZVFAQKXQXRVGXPBETZADVHABVIVCVJHWBVKVLACVMVJVNWRXDIAWOWKA
      LZWRXAXBUBZNXDYBWNYCYBWLXAWMXBWKACJVOWKACVPVQVRXAXBVSVTWCWAWNIWOWDVJWPCWO
      MZWPUIWQYDWPCXIWOHXIWOMXIOMPXIQKZXIUJXJXSXTXFXQYEXPYAFGABWEWFUKUNWGICWHWI
      WJ $.
      $( [18-Feb-2014] $)
  $}

  $( 2 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  2prm $p |- 2 e. Prime $=
    ( vz c2 cprime wcel cuz cfv cv cdivides wbr wn c1 co wral clt 1lt2 mpbir2an
    cz 2z c0 ax-1cn cmin cfz isprm3 eluz2b1 ral0 2cn 1p1e2 subaddrii eqbrtri wb
    wceq 1z eqeltri fzn mp2an mpbi raleqi mpbir ) BCDBBEFDZAGBHIJZABBKUALZUBLZM
    ZABUCUSBQDZKBNIBUDROPVCUTASMUTAUEUTAVBSVABNIZVBSUKZVAKBNBKKUFTTUGUHZOUIVDVA
    QDVEVFUJRVAKQVGULUMBVAUNUOUPUQURP $.
    $( [27-Nov-2012] $) $( [22-Jun-2011] $)

  $( 3 is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  3prm $p |- 3 e. Prime $=
    ( vz c3 cprime wcel c2 cdivides wbr wn c1 co cfz cz clt mpbir2an wi wceq 2z
    ax-mp df-3 mpbir cuz cfv cv cmin wral isprm3 eluz2b1 nnzi 1lt3 caddc iddvds
    3nn cn 2nn 1lt2 ndvdsp1 mp3an breq2i mtbir breq1 mtbiri elfz1eq elfz3 eleq1
    mpbiri impbii imbi1i eqcomi 3cn ax-1cn 2cn subadd2i oveq2i eleq2s rgen ) BC
    DBEUAUBDZAUCZBFGZHZAEBIUDJZKJZUEABUFVPBLDIBMGBUGBULUHUINVSAWAVSVQEEKJZWAVQW
    BDZVSOVQEPZVSOWDVREBFGZWEEEIUJJZFGZEEFGZWGHZELDZWHQEUKRWJEUMDIEMGWHWIOQUNUO
    EEUPUQRBWFEFSURUSVQEBFUTVAWCWDVSWCWDVQEVBWDWCEWBDZWJWKQEVCRVQEWBVDVEVFVGTVT
    EEKVTEPWFBPBWFSVHBIEVIVJVKVLTVMVNVON $.
    $( [23-Nov-2012] $) $( [22-Jun-2011] $)

  ${
    $d P x $.
    $( A prime number is an integer greater than or equal to 2.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    prmuz2 $p |- ( P e. Prime -> P e. ( ZZ>= ` 2 ) ) $=
      ( vx cprime wcel c2 cuz cfv cv cdivides wbr wceq wi wral isprm4 simplbi )
      ACDAEFGZDBHZAIJQAKLBPMBANO $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d N z $.  $d P z $.
    $( An integer greater than or equal to 2 divides a prime number iff it is
       equal to it.  (Contributed by Paul Chapman, 26-Oct-2012.) $)
    dvdsprm $p |- ( ( N e. ( ZZ>= ` 2 ) /\ P e. Prime ) ->
                    ( N || P <-> N = P ) ) $=
      ( vz c2 cuz cfv wcel cprime wa cdivides wbr wceq wi cv wral simprbi breq1
      isprm4 eqeq1 imbi12d rcla4v mpan9 ancoms cz iddvds breq2 syl5ibcom adantr
      eluzelz syl impbid ) BDEFZGZAHGZIBAJKZBALZUNUMUOUPMZUNCNZAJKZURALZMZCULOZ
      UMUQUNAULGVBCARPVAUQCBULURBLUSUOUTUPURBAJQURBASTUAUBUCUMUPUOMZUNUMBUDGZVC
      DBUIVDBBJKUPUOBUEBABJUFUGUJUHUK $.
      $( [26-Oct-2012] $)
  $}

  ${
    $d N z $.  $d P z $.
    $( A prime number either divides an integer or is coprime to it, but not
       both.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    coprm $p |- ( ( P e. Prime /\ N e. ZZ ) ->
                  ( -. P || N <-> ( P gcd N ) = 1 ) ) $=
      ( vz wcel cz wa cdivides wbr wn c1 wceq sylan simprd wo cn cc0 adantr syl
      wi cr cprime cgcd prmz gcddvds breq1 syl5ibcom con3d prmnn eleq1 intnanrd
      co 0nnn mtoi gcdn0cl ex mpd simpld cv wral cuz cfv isprm2 simprbi orbi12d
      c2 eqeq1 imbi12d rcla4v syl5com mp2d biorf syl6bb syl5ibrcom syld cle clt
      orcom wne iddvds w3a dvdslegcd 3anidm12 mpand prmuz2 eluz2b1 zre nnre 1re
      sylib ltletr mp3an1 syl2anc ltne 3expia sylancr 3syld necon2bd impbid ) A
      UADZBEDZFZABGHZIZABUBUKZJKZXAXCXDAKZIZXEXAXFXBXAXDBGHZXFXBXAXDAGHZXHWSAED
      ZWTXIXHFAUCZABUDLZMXDABGUEUFUGXAXEXGXEXFNZXAXDODZXIXMXAAPKZBPKZFIZXNWSXQW
      TWSXOXPWSXOPODZULWSAODXOXRAUHAPOUIUFUMUJQZWSXJWTXQXNSXKXJWTFXQXNABUNUOLUP
      ZXAXIXHXLUQWSXNXIXMSZSWTWSCURZAGHZYBJKZYBAKZNZSZCOUSZXNYAWSAVEUTVADZYHCAV
      BVCYGYACXDOYBXDKZYCXIYFXMYBXDAGUEYJYDXEYEXFYBXDJVFYBXDAVFVDVGVHVIQVJXGXEX
      FXENXMXFXEVKXFXEVQVLVMVNXAXBXDJXAXBAXDVOHZJXDVPHZXDJVRZXAAAGHZXBYKWSYNWTW
      SXJYNXKAVSRQXAXQYNXBFYKSZXSWSXJWTXQYOSZXKXJWTYPXJXJWTVTXQYOAABWAUOWBLUPWC
      XAJAVPHZYKYLWSYQWTWSXJYQWSYIXJYQFAWDAWEWIMQXAATDZXDTDZYQYKFYLSZWSYRWTWSXJ
      YRXKAWFRQXAXNYSXTXDWGRZJTDZYRYSYTWHJAXDWJWKWLWCXAUUBYSYLYMSWHUUAUUBYSYLYM
      JXDWMWNWOWPWQWR $.
      $( [23-Nov-2012] $) $( [22-Jun-2011] $)
  $}

  $( Unequal prime numbers are relatively prime.  (Contributed by Mario
     Carneiro, 23-Feb-2014.) $)
  prmrp $p |- ( ( P e. Prime /\ Q e. Prime ) ->
                ( ( P gcd Q ) = 1 <-> P =/= Q ) ) $=
    ( cprime wcel wa cdivides wbr wn cgcd co c1 wceq cz wb prmz coprm sylan2 c2
    wne cuz cfv prmuz2 dvdsprm sylan necon3bbid bitr3d ) ACDZBCDZEZABFGZHZABIJK
    LZABSUHUGBMDUKULNBOABPQUIUJABUGARTUADUHUJABLNAUBBAUCUDUEUF $.
    $( [23-Feb-2014] $)

  $( If an integer divides the product of two integers and is coprime to one of
     them, then it divides the other.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  coprmdvds $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || ( M x. N ) /\ ( K gcd M ) = 1 ) -> K || N ) ) $=
    ( cz wcel cmul co cdivides wbr cgcd c1 wceq wi wa zcn ancoms 3adant2 adantr
    cc cabs w3a mulcom syl2an 3adant1 breq2d simp1 zmulcl dvdsgcd syl3anc mpand
    dvdsmul2 sylbid cfv absmulgcd 3coml oveq2 mulid1 sylan9eqr fveq2d 3ad2antl3
    syl eqtrd wb dvdsabsb bitr4d sylibd ex com23 imp3a ) ADEZBDEZCDEZUAZABCFGZH
    IZABJGZKLZACHIZVMVQVOVRVMVQVOVRMVMVQNZVOACAFGZCBFGZJGZHIZVRVMVOWCMVQVMVOAWA
    HIZWCVMVNWAAHVKVLVNWALZVJVKBSECSEZWEVLBOCOZBCUBUCUDUEVMAVTHIZWDWCVJVLWHVKVL
    VJWHCAUKPQVMVJVTDEZWADEZWHWDNWCMVJVKVLUFVJVLWIVKVLVJWICAUGPQVKVLWJVJVLVKWJC
    BUGPUDAVTWAUHUIUJULRVSWCACTUMZHIZVRVSWBWKAHVSWBCVPFGZTUMZWKVMWBWNLZVQVLVJVK
    WOCABUNUORVLVJVQWNWKLVKVLVQNWMCTVQVLWMCKFGZCVPKCFUPVLWFWPCLWGCUQVAURUSUTVBU
    EVMVRWLVCZVQVJVLWQVKACVDQRVEVFVGVHVI $.
    $( [22-Jun-2011] $)
  ${
    $d x K $.  $d x M $.  $d x N $.
    $( If an integer is divisible by two coprime integers, than it is divisible
       by their product.  (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    coprmdvds2 $p |- ( ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) /\
                         ( M gcd N ) = 1 ) ->
                       ( ( M || K /\ N || K ) -> ( M x. N ) || K ) ) $=
      ( vx cz wcel w3a cgcd co wceq wa cdivides wbr cmul wi cc zcn sylbid breq2
      syl3anc c1 cv wrex wb divides 3adant1 adantr simprr simpl2 mulcom syl2anc
      syl2an breq2d simprl simpl1 coprmdvds mpan2d dvdsmulc syl5ibcom rexlimdva
      syld imbi12d anassrs com23 imp3a ) BEFZCEFZAEFZGZBCHIUAJZKZBALMZCALMZBCNI
      ZALMZVKVMVLVOVKVMDUBZCNIZAJZDEUCZVLVOOZVIVMVSUDZVJVGVHWAVFDCAUEUFUGVKVRVT
      DEVIVJVPEFZVRVTOVIVJWBKZKZBVQLMZVNVQLMZOVRVTWDWEBVPLMZWFWDWEBCVPNIZLMZWGW
      DVQWHBLWDWBVGVQWHJZVIVJWBUHZVFVGVHWCUIZWBVPPFCPFWJVGVPQCQVPCUJULUKUMWDWIV
      JWGVIVJWBUNWDVFVGWBWIVJKWGOVFVGVHWCUOZWLWKBCVPUPTUQRWDVFWBVGWGWFOWMWKWLCB
      VPURTVAVRWEVLWFVOVQABLSVQAVNLSVBUSVCUTRVDVE $.
      $( [24-Feb-2014] $)
  $}

  $( Two equal reduced fractions have the same numerator and denominator.
     (Contributed by Jeff Hankins, 29-Sep-2013.) $)
  qredeq $p |- ( ( ( M e. ZZ /\ N e. NN /\ ( M gcd N ) = 1 ) /\ ( P e. ZZ
    /\ Q e. NN /\ ( P gcd Q ) = 1 ) /\ ( M / N ) = ( P / Q ) ) -> ( M = P /\
    N = Q ) ) $=
    ( cz wcel co c1 wceq cmul adantr adantl syl3anc 3adant3 3ad2ant2 syl2an wbr
    wa cc cdivides cn cgcd w3a cdiv cc0 wne zcn nncn nnne0 divcan2 eqeq1d divcl
    mulcan syl112anc 3ad2ant1 mulcl mulcan2 mulass divcan1 oveq2d eqeq2d bitr3d
    wb eqtrd 3bitr3d cle cr nnre ad2antrr ad2antlr letri3 syl2anc simp2 anim12i
    nnz simpl1 3jca simp1 dvdsmul1 simpr breqtrrd gcdcom sylan ancoms simp3 jca
    coprmdvds sylc dvdsle simpr1 dvdsmul2 mulcom breqtrd mpbir2and oveq2 anbi2d
    bitrd biimpa syl6bir com12 ancrd mpd ex sylbid 3impia ) CEFZDUAFZCDUBGZHIZU
    CZAEFZBUAFZABUBGZHIZUCZCDUDGZABUDGZIZCAIZDBIZRZXJXORZXRCBJGZDAJGZIZYAYBDXPJ
    GZDXQJGZIZCYGIZXRYEYBYFCYGXJYFCIZXOXFXGYJXIXFXGRZCSFZDSFZDUEUFZYJXFYLXGCUGZ
    KZXGYMXFDUHZLZXGYNXFDUIZLZCDUJMNKUKYBXPSFZXQSFZYMYNYHXRVCXJUUAXOXFXGUUAXIYK
    YLYMYNUUAYPYRYTCDULMNKXOUUBXJXKXLUUBXNXKXLRZASFZBSFZBUEUFZUUBXKUUDXLAUGZKZX
    LUUEXKBUHZLZXLUUFXKBUIZLZABULMNZLZXJYMXOXGXFYMXIYQOZKZXJYNXOXGXFYNXIYSOKZXP
    XQDUMUNYBYCYGBJGZIZYIYEYBYLYGSFZUUEUUFUUSYIVCXJYLXOXFXGYLXIYOUOKZXJYMUUBUUT
    XOUUOUUMDXQUPPXOUUEXJXLXKUUEXNUUIOLZXOUUFXJXLXKUUFXNUUKOLCYGBUQUNYBUURYDYCY
    BUURDXQBJGZJGZYDYBYMUUBUUEUURUVDIUUPUUNUVBDXQBURMYBUVCADJXOUVCAIZXJXKXLUVEX
    NUUCUUDUUEUUFUVEUUHUUJUULABUSMNLUTVDVAVBVEYBYEYAYBYERZXTYAUVFXTDBVFQZBDVFQZ
    UVFDVGFZBVGFZXTUVGUVHRVCXJUVIXOYEXGXFUVIXIDVHOVIXOUVJXJYEXLXKUVJXNBVHOVJDBV
    KVLUVFDEFZXLRZDBTQZUVGYBUVLYEXJUVKXOXLXGXFUVKXIDVOZOZXKXLXNVMVNKUVFUVKXFBEF
    ZUCZDYCTQZDCUBGZHIZRUVMYBUVQYEYBUVKXFUVPXJUVKXOUVOKZXFXGXIXOVPXOUVPXJXLXKUV
    PXNBVOZOZLZVQKUVFUVRUVTUVFDYDYCTYBDYDTQZYEXJUVKXKUWEXOUVOXKXLXNVRDAVSPKYBYE
    VTZWAXJUVTXOYEXJUVSXHHXFXGUVSXHIZXIXGXFUWGXGUVKXFUWGUVNDCWBWCWDNXFXGXIWEVDV
    IWFDCBWGWHDBWIWHUVFUVPXGRZBDTQZUVHYBUWHYEXOXJUWHXOUVPXJXGUWCXFXGXIVMVNWDKUV
    FUVPXKUVKUCZBADJGZTQZBAUBGZHIZRUWIYBUWJYEYBUVPXKUVKUWDXJXKXLXNWJUWAVQKUVFUW
    LUWNUVFBYCUWKTYBBYCTQZYEXJXFUVPUWOXOXFXGXIVRUWCCBWKPKUVFYCYDUWKUWFYBYDUWKIZ
    YEXJYMUUDUWPXOUUOXKXLUUDXNUUGUOZDAWLPKVDWMXOUWNXJYEXOUWMXMHXKXLUWMXMIZXNXLX
    KUWRXLUVPXKUWRUWBBAWBWCWDNXKXLXNWEVDVJWFBADWGWHBDWIWHWNUVFXTXSXTUVFXSXTUVFY
    BCDJGZYDIZRXSXTUWTYEYBXTUWSYCYDDBCJWOUKWPYBUWTXSYBUWTDCJGZYDIZXSYBUWSUXAYDX
    JUWSUXAIZXOXFXGUXCXIXFYLYMUXCXGYOYQCDWLPNKUKYBYLUUDYMYNUXBXSVCUVAXOUUDXJUWQ
    LUUPUUQCADUMUNWQWRWSWTXAXBXCXDXE $.
    $( [29-Sep-2013] $)

  ${
    $d k n x y z A $.
    $( Every rational number has a unique reduced form.  (Contributed by Jeff
       Hankins, 29-Sep-2013.) $)
    qredeu $p |- ( A e. QQ -> E! x e. ( ZZ X. NN ) ( ( ( 1st ` x ) gcd ( 2nd `
    x ) ) = 1 /\ A = ( ( 1st ` x ) / ( 2nd ` x ) ) ) ) $=
      ( cdiv co wceq cn cz c1st cfv c2nd cgcd c1 wa wcel wbr cc0 adantl syl3anc
      syl cc vz vn vy cv wrex cxp wral wreu w3a cop cdivides nnz gcddvds simpld
      wi cq sylan2 wne wb gcdcl nn0z wn simpl nnne0 df-ne sylib intnand gcdn0cl
      cn0 syl21anc divides2 mpbid 3adant3 clt simprd cr nnre nngt0 nn0re divgt0
      syl22anc jca elnnz sylibr opelxpi syl2anc cmul mulid1 3syl adantr divcan2
      nn0cn zcn nncn oveq12d mulgcd 3eqtr2rd ax-1cn syl112anc divcan7 syl122anc
      a1i mulcan eqeq2d biimp3ar fveq2 ovex op1st syl6eq eqeq1d anbi12d rcla4ev
      op2nd syl12anc elxp6 simprl simprr simprll simprrl simprlr simprrr eqtr3d
      ad2antrr ad2antlr qredeq syl331anc fvex simplll simplrl 3eqtr4d ex rgen2a
      opth syl2anb jctir 3expia rexlimivv elq reu4 3imtr4i ) BUAUDZUBUDZCDZEZUB
      FUEUAGUEAUDZHIZUUEJIZKDZLEZBUUFUUGCDZEZMZAGFUFZUEZUULUCUDZHIZUUOJIZKDZLEZ
      BUUPUUQCDZEZMZMZUUEUUOEZUOZUCUUMUGAUUMUGZMZBUPNUULAUUMUHUUDUVGUAUBGFUUAGN
      ZUUBFNZUUDUVGUVHUVIUUDUIZUUNUVFUVJUUAUUAUUBKDZCDZUUBUVKCDZUJZUUMNZUVLUVMK
      DZLEZBUVLUVMCDZEZUUNUVJUVLGNZUVMFNZUVOUVHUVIUVTUUDUVHUVIMZUVKUUAUKOZUVTUV
      IUVHUUBGNZUWCUUBULZUVHUWDMZUWCUVKUUBUKOZUUAUUBUMZUNUQUWBUVKGNZUVKPURZUVHU
      WCUVTUSUWBUVKVINZUWIUVIUVHUWDUWKUWEUUAUUBUTUQZUVKVASZUWBUVKFNZUWJUWBUVHUW
      DUUAPEZUUBPEZMVBZUWNUVHUVIVCZUVIUWDUVHUWEQZUVIUWQUVHUVIUWPUWOUVIUUBPURZUW
      PVBUUBVDZUUBPVEVFVGQUUAUUBVHVJZUVKVDSZUWRUVKUUAVKRVLZVMUVJUVMGNZPUVMVNOZM
      ZUWAUVHUVIUXGUUDUWBUXEUXFUWBUWGUXEUVIUVHUWDUWGUWEUWFUWCUWGUWHVOUQUWBUWIUW
      JUWDUWGUXEUSUWMUXCUWSUVKUUBVKRVLZUWBUUBVPNZPUUBVNOZUVKVPNZPUVKVNOZUXFUVIU
      XIUVHUUBVQQUVIUXJUVHUUBVRQUWBUWKUXKUWLUVKVSSUWBUWNUXLUXBUVKVRSUUBUVKVTWAW
      BVMUVMWCWDUVLUVMGFWEWFUVHUVIUVQUUDUWBUVKUVPWGDZUVKLWGDZEZUVQUWBUXNUVKUVKU
      VLWGDZUVKUVMWGDZKDZUXMUWBUWKUVKTNZUXNUVKEUWLUVKWLZUVKWHWIUWBUXPUUAUXQUUBK
      UWBUUATNZUXSUWJUXPUUAEUVHUYAUVIUUAWMWJZUWBUWKUXSUWLUXTSZUXCUUAUVKWKRUWBUU
      BTNZUXSUWJUXQUUBEUVIUYDUVHUUBWNQZUYCUXCUUBUVKWKRWOUWBUWKUVTUXEUXRUXMEUWLU
      XDUXHUVKUVLUVMWPRWQUWBUVPTNZLTNZUXSUWJUXOUVQUSUWBUVPVINZUYFUWBUVTUXEUYHUX
      DUXHUVLUVMUTWFUVPWLSUYGUWBWRXBUYCUXCUVPLUVKXCWSVLVMUVHUVIUVSUUDUWBUVRUUCB
      UWBUYAUYDUWTUXSUWJUVRUUCEUYBUYEUVIUWTUVHUXAQUYCUXCUUAUUBUVKWTXAXDXEUULUVQ
      UVSMAUVNUUMUUEUVNEZUUIUVQUUKUVSUYIUUHUVPLUYIUUFUVLUUGUVMKUYIUUFUVNHIUVLUU
      EUVNHXFUVLUVMUUAUVKCXGZXHXIZUYIUUGUVNJIUVMUUEUVNJXFUVLUVMUYJUUBUVKCXGXMXI
      ZWOXJUYIUUJUVRBUYIUUFUVLUUGUVMCUYKUYLWOXDXKXLXNUVEAUCUUMUUEUUMNUUEUUFUUGU
      JZEZUUFGNZUUGFNZMZMZUUOUUPUUQUJZEZUUPGNZUUQFNZMZMZUVEUUOUUMNUUEGFXOUUOGFX
      OUYRVUDMZUVCUVDVUEUVCMZUYMUYSUUEUUOVUFUUFUUPEUUGUUQEMZUYMUYSEVUFUYOUYPUUI
      VUAVUBUUSUUJUUTEVUGUYRUYOVUDUVCUYNUYOUYPXPYCUYRUYPVUDUVCUYNUYOUYPXQYCVUEU
      UIUUKUVBXRVUDVUAUYRUVCUYTVUAVUBXPYDVUDVUBUYRUVCUYTVUAVUBXQYDVUEUULUUSUVAX
      SVUFBUUJUUTVUEUUIUUKUVBXTVUEUULUUSUVAYAYBUUPUUQUUFUUGYEYFUUFUUGUUPUUQUUEH
      YGUUEJYGUUOJYGYMWDUYNUYQVUDUVCYHUYRUYTVUCUVCYIYJYKYNYLYOYPYQUAUBBYRUULUVB
      AUCUUMUVDUUIUUSUUKUVAUVDUUHUURLUVDUUFUUPUUGUUQKUUEUUOHXFZUUEUUOJXFZWOXJUV
      DUUJUUTBUVDUUFUUPUUGUUQCVUHVUIWOXDXKYSYT $.
      $( [29-Sep-2013] $)
  $}

  $( Euclid's lemma.  A prime number divides the product of two integers iff it
     divides at least one of them.  (Contributed by Paul Chapman,
     17-Nov-2012.) $)
  euclemma $p |- ( ( P e. Prime /\ M e. ZZ /\ N e. ZZ ) ->
                        ( P || ( M x. N ) <-> ( P || M \/ P || N ) ) ) $=
    ( cprime wcel cz w3a cmul co cdivides wbr wo wn wi wa cgcd c1 wceq syl3an1
    wb coprm 3adant3 anbi2d prmz coprmdvds sylbid exp3a df-or syl6ibr ordvdsmul
    impbid ) ADEZBFEZCFEZGZABCHIJKZABJKZACJKZLZUOUPUQMZURNUSUOUPUTURUOUPUTOUPAB
    PIQRZOZURUOUTVAUPULUMUTVATUNABUAUBUCULAFEZUMUNVBURNAUDZABCUESUFUGUQURUHUIUL
    VCUMUNUSUPNVDABCUJSUK $.
    $( [17-Nov-2012] $)

  ${
    $d N m $.  $d S x y $.  $d m x y $.
    exprmfctlem.1 $e |- S = { m e. ( ZZ>= ` 2 ) | m || N } $.
    exprmfctlem.2 $e |- N e. ( ZZ>= ` 2 ) $.
    $( Lemma for ~ exprmfct . $)
    exprmfctlem $p |- E. x e. Prime x || N $=
      ( vy cv wbr cdivides cprime c2 wcel cz eluzelz ax-mp wa breq1 wi syl wral
      cle wrex cuz cfv wss c0 wne crab ssrab2 eqsstri iddvds elrab2 ne0i sylbir
      mp2an uzwo w3a simprbi adantl sseli dvdstr mp3an3 syl2an mpan2d imdistand
      wceq expcom syl6ibr 3impib cn c1 eluz2b3 simplbi dvdsle cr wb nnre letri3
      eluzelre exp3a syld 3imp imim1 syl5 mpdi 3expd com12 ralimdv2 isprm4 baib
      biimprd sylibrd imp adantr jca reximi2 ) AHZGHZUBIZGBUAZABUCZWRDJIZAKUCBL
      UDUEZUFBUGUHZXBBCHZDJIZCXDUIXDEXGCXDUJUKZDXDMZDDJIZXEFDNMZXJXIXKFLDOPZDUL
      PXIXJQDBMXEXGXJCDXDBXFDDJREUMBDUNUOUPBAGLUQUPXAXCABKWRBMZXAQWRKMZXCXMXAXN
      XMXAWSWRJIZWSWRVGZSZGXDUAZXNXMWTXQGBXDWSBMZWTSZXMWSXDMZXQSXTXMYAXOXPXTXMY
      AXOURZXSXPXMYAXOXSXMYAXOQYAWSDJIZQXSXMYAXOYCYAXMXOYCSYAXMQZXOXCYCXMXCYAXM
      WRXDMZXCXGXCCWRXDBXFWRDJREUMUSZUTYAWSNMZWRNMZXOXCQYCSZXMLWSOZXMYEYHBXDWRX
      HVAZLWROTYGYHXKYIXLWSWRDVBVCVDVEVHVFXGYCCWSXDBXFWSDJREUMVIVJYBWTXPSZXTXSX
      PSXMYAXOYLYAXMXOYLSYDXOWSWRUBIZYLYAYGWRVKMZXOYMSXMYJXMYEYNYKYEYNWRVLUHWRV
      MVNTZWSWRVOVDYDYMWTXPYDXPYMWTQZYAWSVPMWRVPMZXPYPVQXMLWSVTXMYNYQYOWRVRTWSW
      RVSVDWLWAWBVHWCXSWTXPWDWEWFWGWHWIXMYEXNXRVQYKXNYEXRGWRWJWKTWMWNXMXCXAYFWO
      WPWQP $.
      $( [23-Nov-2012] $) $( [26-Oct-2012] $)
  $}

  ${
    $d N m p $.
    $( Every integer greater than or equal to 2 has a prime factor.
       (Contributed by Paul Chapman, 26-Oct-2012.) $)
    exprmfct $p |- ( N e. ( ZZ>= ` 2 ) -> E. p e. Prime p || N ) $=
      ( vm c2 cuz cfv wcel cdivides wbr cprime wrex cif wceq breq2 rexbidv crab
      cv eqid cz 2z uzid ax-mp elimel exprmfctlem dedth ) ADEFZGZBQZAHIZBJKUHUG
      ADLZHIZBJKADAUJMUIUKBJAUJUHHNOBCQUJHICUFPZCUJULRADUFDSGDUFGTDUAUBUCUDUE
      $.
      $( [26-Oct-2012] $)
  $}

  $( No prime number divides 1.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  nprmdvds1 $p |- ( P e. Prime -> -. P || 1 ) $=
    ( cprime wcel c1 cdivides wbr clt cz c2 cuz cfv prmuz2 eluz2b1 sylib simprd
    wa cle wn cn cr wi prmz 1nn dvdsle sylancl wb zre 1re lenlt mpan2 3syl mt2d
    sylibd ) ABCZADEFZDAGFZUNAHCZUPUNAIJKCUQUPPALAMNOUNUOADQFZUPRZUNUQDSCUOURUA
    AUBZUCADUDUEUNUQATCZURUSUFZUTAUGVADTCVBUHADUIUJUKUMUL $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d x z P $.
    $( One need only check prime divisors of ` P ` up to ` sqr P ` in order to
       ensure primality.  (Contributed by Mario Carneiro, 18-Feb-2014.) $)
    isprm5 $p |- ( P e. Prime <-> ( P e. ( ZZ>= ` 2 ) /\
                    A. z e. Prime ( ( z ^ 2 ) <_ P -> -. z || P ) ) ) $=
      ( cprime wcel c2 wbr wceq wi wa co cle wn cmul clt c1 cr cc0 syl syl2anc
      wb vx cuz cfv cv cdivides wral cexp isprm4 prmuz2 a1i cn eluz2b2 eluzelre
      simprbi simplbi nngt0 ltmulgt11 syl3anc mpbid oveq12 anidms breq1d notbid
      ax-mulrcl ltnle syl5ibrcom con2 syl6 imim12d ralimdv2 wrex annim ad2antlr
      imim2d wo ad2antrr letric weq anbi12d rcla4ev ancom2s expr ad2ant2lr cdiv
      breq1 cz simprl wne eluzelz nnne0 divides2 cc recnd mulid2 ltlen syl21anc
      dvdsle imp simprr df-ne sylibr necomd mpbir2and eqbrtrd 1re nnre ltmuldiv
      zre jca eluz2b1 sylanbrc nnmulcl nnrp rpdivcl syl2an elrp sylib divmuldiv
      lemul1 syl22anc nncn divass syl112anc eqtrd divcan2 eqcomd breq12d bitr4d
      crp biimpd mpd ex rexlimdva ad2antrl remulcl 3syl cn0 nnnn0 nn0ge0 rexnal
      dvds0lem syl31anc jctird ee12an jaod syl5bir exprmfct prmz simpllr le2msq
      ad3antrrr simplrl letrd simplrr dvdstr mp2and jc reximdva syld impcon4bid
      3imtr3g prmnn sqval imbi1d ralbiia syl6bbr pm5.32i bitri ) BCDBEUBUCZDZAU
      DZBUEFZUVKBGZHZAUVIUFZIUVJUVKEUGJZBKFZUVLLZHZACUFZIABUHUVJUVOUVTUVJUVOUVK
      UVKMJZBKFZUVRHZACUFZUVTUVJUVOUWDUVJUVNUWCAUVICUVJUVKCDZUVKUVIDZUVNUWCUWEU
      WFHUVJUVKUIZUJUVJUVNUVLUWBLZHUWCUVJUVMUWHUVLUVJUWHUVMBBMJZBKFZLZUVJBUWINF
      ZUWKUVJOBNFZUWLUVJBUKDZUWMBULZUNUVJBPDZUWPQBNFZUWMUWLTEBUMZUWRUVJUWNUWQUV
      JUWNUWMUWOUOZBUPRBBUQURUSUVJUWPUWIPDZUWLUWKTUWRUVJUWPUWPUWTUWRUWRBBVDSBUW
      IVESUSUVMUWBUWJUVMUWAUWIBKUVMUWAUWIGUVKBUVKBMUTVAVBVCVFVNUVLUWBVGVHVIVJUV
      JUVNLZAUVIVKZUWCLZACVKZUVOLUWDLUVJUXBUAUDZUXEMJZBKFZUXEBUEFZIZUAUVIVKZUXD
      UVJUXAUXJAUVIUXAUVLUVMLZIZUVJUWFIZUXJUVLUVMVLUXMUXLUXJUXMUXLIZUWBBUWAKFZV
      OZUXJUXNUWAPDZUWPUXPUXNUVKPDZUXRUXQUWFUXRUVJUXLEUVKUMVMZUXSUVKUVKVDSZUVJU
      WPUWFUXLUWRVPZUWABVQSUXNUWBUXJUXOUWFUVLUWBUXJHUVJUXKUWFUVLUWBUXJUWFUWBUVL
      UXJUXIUWBUVLIUAUVKUVIUAAVRZUXGUWBUXHUVLUYBUXFUWABKUYBUXFUWAGUXEUVKUXEUVKM
      UTVAVBUXEUVKBUEWEVSVTWAWBWCUXNBUVKWDJZUVIDZUXOUYCUYCMJZBKFZUYCBUEFZIZUXJU
      XNUYCWFDZOUYCNFZUYDUXNUVLUYIUXMUVLUXKWGZUXNUVKWFDZUVKQWHZBWFDZUVLUYITUWFU
      YLUVJUXLEUVKWIVMZUXNUVKUKDZUYMUWFUYPUVJUXLUWFUYPOUVKNFUVKULUOZVMZUVKWJRZU
      VJUYNUWFUXLEBWIZVPZUVKBWKURUSZUXNOUVKMJZBNFZUYJUXNVUCUVKBNUXNUVKWLDZVUCUV
      KGUXNUVKUXSWMZUVKWNRUXNUVKBNFZUVKBKFZBUVKWHZUXNUXRUWPVUGVUHVUIITUXSUYAUVK
      BWOSUXNUYLUWNUVLVUHUYOUVJUWNUWFUXLUWSVPZUYKUYLUWNIUVLVUHUVKBWQWRWPUXNUVKB
      UXNUXKUVKBWHUXMUVLUXKWSUVKBWTXAXBXCXDUXNOPDZUWPUXRQUVKNFZIZVUDUYJTVUKUXNX
      EUJUXNUYNUWPVUABXHZRUXNUYPVUMUYRUYPUXRVULUVKXFUVKUPXIROBUVKXGURUSUYCXJXKU
      XNUXOUYFUYGUXNUXOUYFUXNUXOBBUWAWDJZMJZUWAVUOMJZKFZUYFUXNUWPUXQVUOPDQVUONF
      IZUXOVURTUYAUXTUXNVUOYIDZVUSUXNUWNUWAUKDZVUTVUJUXNUYPUYPVVAUYRUYRUVKUVKXL
      SZUWNBYIDUWAYIDVUTVVABXMUWAXMBUWAXNXOSVUOXPXQBUWAVUOXSURUXNUYEVUPBVUQKUXN
      UYEUWIUWAWDJZVUPUXNBWLDZVVDVUEUYMIZVVEUYEVVCGUXNBUYAWMZVVFUXNVUEUYMVUFUYS
      XIZVVGBBUVKUVKXRXTUXNVVDVVDUWAWLDZUWAQWHZVVCVUPGVVFVVFUXNVVAVVHVVBUWAYARZ
      UXNVVAVVIVVBUWAWJRZBBUWAYBYCYDUXNVUQBUXNVVDVVHVVIVUQBGVVFVVJVVKBUWAYEURYF
      YGYHYJUXNUYLUYIUYNUVKUYCMJBGZUYGUYOVUBVUAUXNVVDVUEUYMVVLVVFVUFUYSBUVKYEUR
      UVKUYCBUUAUUBUUCUXIUYHUAUYCUVIUXEUYCGZUXGUYFUXHUYGVVMUXFUYEBKVVMUXFUYEGUX
      EUYCUXEUYCMUTVAVBUXEUYCBUEWEVSVTUUDUUEYKYLUUFYMUVJUXIUXDUAUVIUVJUXEUVIDZI
      ZUXIUXDVVOUXIIZUVKUXEUEFZACVKZUXDVVNVVRUVJUXIUXEAUUGVMVVPVVQUXCACVVPUWEVV
      QUXCVVPUWEVVQIZIZUWBUVLVVTUWAUXFBVVTUXRUXRUXQVVTUYLUXRUWEUYLVVPVVQUVKUUHY
      NZUVKXHRZVWBUVKUVKYOSVVTUXEPDZVWCUXFPDVVTVVNUXEWFDZVWCUVJVVNUXIVVSUUIZEUX
      EWIZUXEXHYPZVWGUXEUXEYOSVVTUYNUWPUVJUYNVVNUXIVVSUYTUUKZVUNRVVTUVKUXEKFZUW
      AUXFKFZVVTUYLUXEUKDZVVQVWIVWAVVTVVNVWKVWEVVNVWKOUXENFUXEULUOZRVVPUWEVVQWS
      ZUYLVWKIVVQVWIUVKUXEWQWRWPVVTUXRQUVKKFZVWCQUXEKFZVWIVWJTVWBUWEVWNVVPVVQUW
      EUWFVWNUWGUWFUYPUVKYQDVWNUYQUVKYRUVKYSYPRYNVWGVVTVVNVWKVWOVWEVWLVWKUXEYQD
      VWOUXEYRUXEYSRYPUVKUXEUUJXTUSVVOUXGUXHVVSUULUUMVVTVVQUXHUVLVWMVVOUXGUXHVV
      SUUNVVTUYLVWDUYNVVQUXHIUVLHVWAVVTVVNVWDVWEVWFRVWHUVKUXEBUUOURUUPUUQWBUURY
      KYLYMUUSUVNAUVIYTUWCACYTUVAUUTUVSUWCACUWEUVQUWBUVRUWEUVPUWABKUWEUYPVUEUVP
      UWAGUVKUVBUVKYAUVKUVCYPVBUVDUVEUVFUVGUVH $.
      $( [18-Feb-2014] $)
  $}

  ${
    $d N x y $.  $d N y z $.  $d S x y $.
    maxprmfct.1 $e |- S = { z e. Prime | z || N } $.
    $( The set of prime factors of an integer greater than or equal to 2
       satisfies the conditions to have a supremum, and that supremum is a
       member of the set.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
    maxprmfct $p |- ( N e. ( ZZ>= ` 2 ) ->
         ( ( S C_ RR /\ S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) /\
         sup ( S , RR , < ) e. S ) ) $=
      ( c2 wcel cr wss wne cv cle wbr wral wrex cz cprime cdivides sstri cuz c0
      cfv w3a clt csup crab ssrab2 eqsstri prmz ssriv zssre a1i exprmfct wex wa
      breq1 elrab2 exbii n0 df-rex 3bitr4ri sylib eluzelre cn c1 eluz2b3 anim1i
      simplbi sylbi wi dvdsle expcom imp3a syl5 ralrimiv syl wceq breq2 ralbidv
      rcla4ev syl2anc 3jca suprzcl mp3an1 jca ) EGUAUCHZDIJZDUBKZBLZALZMNZBDOZA
      IPZUDDIUEUFDHZWGWHWIWNWHWGDQIDRQDCLZESNZCRUGRFWQCRUHUIBRQWJUJZUKTZULTUMWG
      WJESNZBRPZWIEBUNWJDHZBUOWJRHZWTUPZBUOWIXAXBXDBWQWTCWJRDWPWJESUQFURZUSBDUT
      WTBRVAVBVCZWGEIHWJEMNZBDOZWNGEVDWGEVEHZXHWGXIEVFKEVGVIXIXGBDXBWJQHZWTUPZX
      IXGXBXDXKXEXCXJWTWRVHVJXIXJWTXGXJXIWTXGVKWJEVLVMVNVOVPVQWMXHAEIWKEVRWLXGB
      DWKEWJMVSVTWAWBZWCWGWIWNWOXFXLDQJWIWNWOWSABDWDWEWBWF $.
      $( [23-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d N m $.  $d P k m $.  $d A k m $.
    $( A prime divides a positive power of an integer iff it divides the
       integer.  (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    prmdvdsexp $p |- ( ( P e. Prime /\ A e. ZZ /\ N e. NN ) ->
                       ( P || ( A ^ N ) <-> P || A ) ) $=
      ( vm vk wcel cz cn cexp co cdivides wbr wb wi c1 wceq oveq2 breq2d bibi1d
      imbi2d cprime wa cv caddc cc zcn adantl exp1 syl wo cmul cn0 nnnn0 syl2an
      expp1 simpll simpr zexpcl simplr euclemma bitrd orbi1 oridm syl6bb bibi2d
      syl3anc syl5ibcom expcom a2d nnind impcom 3impa ) BUAFZAGFZCHFZBACIJZKLZB
      AKLZMZVOVMVNUBZVSVTBADUCZIJZKLZVRMZNVTBAOIJZKLZVRMZNVTBAEUCZIJZKLZVRMZNVT
      BAWHOUDJZIJZKLZVRMZNVTVSNDECWAOPZWDWGVTWPWCWFVRWPWBWEBKWAOAIQRSTWAWHPZWDW
      KVTWQWCWJVRWQWBWIBKWAWHAIQRSTWAWLPZWDWOVTWRWCWNVRWRWBWMBKWAWLAIQRSTWACPZW
      DVSVTWSWCVQVRWSWBVPBKWACAIQRSTVTWEABKVTAUEFZWEAPVNWTVMAUFUGZAUHUIRWHHFZVT
      WKWOVTXBWKWONVTXBUBZWNWJVRUJZMWKWOXCWNBWIAUKJZKLZXDXCWMXEBKVTWTWHULFZWMXE
      PXBXAWHUMZAWHUOUNRXCVMWIGFZVNXFXDMVMVNXBUPVTVNXGXIXBVMVNUQXHAWHURUNVMVNXB
      USBWIAUTVFVAWKXDVRWNWKXDVRVRUJVRWJVRVRVBVRVCVDVEVGVHVIVJVKVL $.
      $( [17-Jul-2014] $) $( [24-Feb-2014] $)
  $}

  $( A prime divides a positive power of another iff they are equal.
     (Contributed by Paul Chapman, 30-Nov-2012.)  (Revised by Mario Carneiro,
     24-Feb-2014.) $)
  prmdvdsexpb $p |- ( ( P e. Prime /\ Q e. Prime /\ N e. NN ) ->
                      ( P || ( Q ^ N ) <-> P = Q ) ) $=
    ( cprime wcel cn w3a cexp co cdivides wceq cz wb prmz prmdvdsexp syl3an2 c2
    wbr cuz cfv prmuz2 dvdsprm sylan 3adant3 bitrd ) ADEZBDEZCFEZGABCHIJRZABJRZ
    ABKZUGUFBLEUHUIUJMBNBACOPUFUGUJUKMZUHUFAQSTEUGULAUABAUBUCUDUE $.
    $( [24-Feb-2014] $) $( [30-Nov-2012] $)

  ${
    $d P z $.
    $( Two positive prime powers are equal iff the primes and the powers are
       equal.  (Contributed by Paul Chapman, 30-Nov-2012.) $)
    prmexpb $p |- ( ( ( P e. Prime /\ Q e. Prime ) /\ ( M e. NN /\ N e. NN ) )
                    -> ( ( P ^ M ) = ( Q ^ N ) <-> ( P = Q /\ M = N ) ) ) $=
      ( cprime wcel wa cn cexp co wceq w3a cdivides wbr cz prmz mpbid syl nnz
      wb adantr 3ad2ant1 simp2l iddvdsexp syl2anc 3ad2ant3 simp1l simp1r simp2r
      breq2 prmdvdsexpb syl3anc bitrd simp3 oveq1d eqtr4d cr c1 clt zre cuz cfv
      prmuz2 eluz2b1 simprbi ad2antrr 3adant3 expcan syl31anc jca 3expia oveq12
      c2 impbid1 ) AEFZBEFZGZCHFZDHFZGZGACIJZBDIJZKZABKZCDKZGZVQVTWCWFVQVTWCLZW
      DWEWGAWAMNZWDWGAOFZVRWHVQVTWIWCVOWIVPAPUAUBZVQVRVSWCUCZACUDUEWGWHAWBMNZWD
      WCVQWHWLTVTWAWBAMUJUFWGVOVPVSWLWDTVOVPVTWCUGVOVPVTWCUHVQVRVSWCUIZABDUKULU
      MQZWGWAADIJZKZWEWGWAWBWOVQVTWCUNWGABDIWNUOUPWGAUQFZCOFZDOFZURAUSNZWPWETWG
      WIWQWJAUTRWGVRWRWKCSRWGVSWSWMDSRVQVTWTWCVOWTVPVTVOAVMVAVBFZWTAVCXAWIWTAVD
      VERVFVGACDVHVIQVJVKABCDIVLVN $.
      $( [30-Nov-2012] $)
  $}

  ${
    $d P x k $.  $d N x $.
    $( The factorial of a number only contains primes less than the base.
       (Contributed by Mario Carneiro, 6-Mar-2014.) $)
    prmfac1 $p |- ( ( N e. NN0 /\ P e. Prime /\ P || ( ! ` N ) ) -> P <_ N ) $=
      ( wcel cfa cfv cdivides wbr cle wi cc0 c1 wceq fveq2 breq2d breq2 imbi12d
      imbi2d adantr cz syl vx vk cn0 cprime caddc fac0 breq2i nprmdvds1 pm2.21d
      cv co syl5bi wa wo cmul facp1 wb simpr faccl nnz nn0p1nn euclemma syl3anc
      cn bitrd nn0re lep1 prmz adantl zre nnre letr mpan2d imim2d com23 syl2anc
      cr dvdsle a1dd jaod sylbid ex a2d nn0ind 3imp ) BUCCAUDCZABDEZFGZABHGZWFA
      UAUJZDEZFGZAWJHGZIZIWFAJDEZFGZAJHGZIZIWFAUBUJZDEZFGZAWSHGZIZIWFAWSKUEUKZD
      EZFGZAXDHGZIZIWFWHWIIZIUAUBBWJJLZWNWRWFXJWLWPWMWQXJWKWOAFWJJDMNWJJAHOPQWJ
      WSLZWNXCWFXKWLXAWMXBXKWKWTAFWJWSDMNWJWSAHOPQWJXDLZWNXHWFXLWLXFWMXGXLWKXEA
      FWJXDDMNWJXDAHOPQWJBLZWNXIWFXMWLWHWMWIXMWKWGAFWJBDMNWJBAHOPQWPAKFGZWFWQWO
      KAFUFUGWFXNWQAUHUIULWSUCCZWFXCXHXOWFXCXHIXOWFUMZXFXCXGXPXFXAAXDFGZUNZXCXG
      IZXPXFAWTXDUOUKZFGZXRXPXEXTAFXOXEXTLWFWSUPRNXPWFWTSCZXDSCZYAXRUQXOWFURXPW
      TVDCZYBXOYDWFWSUSRWTUTTXPXDVDCZYCXOYEWFWSVARZXDUTTAWTXDVBVCVEXPXAXSXQXPXC
      XAXGXPXBXGXAXPXBWSXDHGZXGXPWSVQCZYGXOYHWFWSVFRZWSVGTXPAVQCZYHXDVQCZXBYGUM
      XGIXPASCZYJWFYLXOAVHVIZAVJTYIXPYEYKYFXDVKTAWSXDVLVCVMVNVOXPXQXGXCXPYLYEXQ
      XGIYMYFAXDVRVPVSVTWAVOWBWCWDWE $.
      $( [6-Mar-2014] $)
  $}

  $( Either ` A / ( A gcd B ) ` is odd or ` B / ( A gcd B ) ` is odd.
     (Contributed by Scott Fenton, 19-Apr-2014.) $)
  divgcdodd $p |- ( ( A e. NN /\ B e. NN ) ->
    ( -. 2 || ( A / ( A gcd B ) ) \/ -. 2 || ( B / ( A gcd B ) ) ) ) $=
    ( cn wcel wa c2 cgcd co cdiv cdivides wbr wn c1 cz nnz cc0 wceq syl2anc syl
    wb wi cprime 2prm nprmdvds1 ax-mp gcddvds syl2an simpld anim12i nnne0 df-ne
    wo wne sylib intnanrd adantr gcdn0cl divides2 syl3anc mpbid simprd dvdsgcdb
    adantl 2z mp3an1 gcddiv syl31anc cc nncn divid eqtr3d breq2d biimpd expdimp
    sylbid mtoi ex imor ) ACDZBCDZEZFAABGHZIHZJKZFBWBIHZJKZLZUAWDLWGULWAWDWGWAW
    DEWFFMJKZFUBDWHLUCFUDUEWAWDWFWHWAWDWFEZFWCWEGHZJKZWHWAWCNDZWENDZWIWKTZWAWBA
    JKZWLWAWOWBBJKZVSANDZBNDZWOWPEZVTAOZBOZABUFUGZUHWAWBNDZWBPUMZWQWOWLTWAWBCDZ
    XCWAWQWREAPQZBPQZELZXEVSWQVTWRWTXAUIVSXHVTVSXFXGVSAPUMXFLAUJAPUKUNUOUPABUQR
    ZWBOSZWAXEXDXIWBUJSZVSWQVTWTUPZWBAURUSUTWAWPWMWAWOWPXBVAWAXCXDWRWPWMTXJXKVT
    WRVSXAVCZWBBURUSUTFNDWLWMWNVDFWCWEVBVERWAWKWHWAWJMFJWAWBWBIHZWJMWAWQWRXEWSX
    NWJQXLXMXIXBABWBVFVGWAWBVHDZXDXNMQWAXEXOXIWBVISXKWBVJRVKVLVMVOVNVPVQWDWGVRU
    N $.
    $( [19-Apr-2014] $) $( [19-Apr-2014] $)

  ${
    $d p A $.  $d p B $.  $d p N $.
    $( If two numbers ` A ` and ` B ` are relatively prime, then they are still
       relatively prime if raised to a power.  (Contributed by Mario Carneiro,
       24-Feb-2014.) $)
    rpexp $p |- ( ( A e. ZZ /\ B e. ZZ /\ N e. NN ) ->
                  ( ( ( A ^ N ) gcd B ) = 1 <-> ( A gcd B ) = 1 ) ) $=
      ( vp cz wcel cc0 wceq wa co cgcd c1 wb wi cdivides wbr cprime syl syl3anc
      wn cn w3a cexp 0exp oveq1d eqeq1d oveq1 sylan bibi12d syl5ibrcom 3ad2ant3
      oveq12 c2 cuz cfv wne cv wrex exprmfct simpl1 simpl3 nnnn0 zexpcl syl2anc
      cn0 adantr simpl2 gcddvds simpld adantl simpr cc zcn expeq0 anbi1d mtbird
      prmz gcdn0cl syl21anc dvdstr mpan2d simpll1 prmdvdsexp sylibd simprd jcad
      nnz dvdsgcd nprmdvds1 breq2 notbid necon2ad 3syld rexlimdva 3adantl3 baib
      eluz2b3 sylibrd syl5 iddvdsexp mp2and impbid 3bitr3d necon4bid ex pm2.61d
      ) AEFZBEFZCUAFZUBZAGHZBGHZIZACUCJZBKJZLHZABKJZLHZMZXIXGXMXSNXHXIXSXMGCUCJ
      ZGKJZLHZGGKJZLHZMXIYAYCLXIXTGGKCUDUEUFXMXPYBXRYDXMXOYALXKXNXTHXLXOYAHAGCU
      CUGXNXTBGKULUHUFXMXQYCLAGBGKULUFUIUJUKXJXMTZXSXJYEIZXOLXQLYFXOUMUNUOZFZXQ
      YGFZXOLUPZXQLUPZYFYHYIYHDUQZXOOPZDQURZYFYIXODUSYFYNYKYIYFYMYKDQYFYLQFZIZY
      MYLAOPZYLBOPZIZYLXQOPZYKYPYMYQYRYPYMYLXNOPZYQYPYMXOXNOPZUUAYPUUBXOBOPZYPX
      NEFZXHUUBUUCIYFUUDYOYFXGCVEFZUUDXGXHXIYEUTZYFXIUUEXGXHXIYEVAZCVBRACVCVDZV
      FZYFXHYOXGXHXIYEVGZVFZXNBVHVDZVIYPYLEFZXOEFZUUDYMUUBIUUANYOUUMYFYLVQVJZYF
      UUNYOYFXOUAFZUUNYFUUDXHXNGHZXLIZTUUPUUHUUJYFUURXMXJYEVKYFUUQXKXLYFAVLFZXI
      UUQXKMYFXGUUSUUFAVMRUUGACVNVDVOVPXNBVRVSZXOWGRVFZUUIYLXOXNVTSWAYPYOXGXIUU
      AYQMYFYOVKXGXHXIYEYOWBZYFXIYOUUGVFZAYLCWCSWDYPYMUUCYRYPUUBUUCUULWEYPUUMUU
      NXHYMUUCIYRNUUOUVAUUKYLXOBVTSWAWFYPUUMXGXHYSYTNUUOUVBUUKYLABWHSYOYTYKNYFY
      OYTXQLYOYTTXRYLLOPZTZYLWIZXRYTUVDXQLYLOWJWKUJWLVJWMWNYFXQUAFZYIYKMXGXHYEU
      VGXIABVRWOZYIUVGYKXQWQWPRZWRWSYIYTDQURZYFYHXQDUSYFUVJYJYHYFYTYJDQYPYTUUAY
      RIZYMYJYPYTUUAYRYPYTXQXNOPZUUAYPXQAOPZAXNOPZUVLYPUVMXQBOPZYPXGXHUVMUVOIUV
      BUUKABVHVDZVIYPXGXIUVNUVBUVCACWTVDYPXQEFZXGUUDUVMUVNIUVLNYFUVQYOYFUVGUVQU
      VHXQWGRVFZUVBUUIXQAXNVTSXAYPUUMUVQUUDYTUVLIUUANUUOUVRUUIYLXQXNVTSWAYPYTUV
      OYRYPUVMUVOUVPWEYPUUMUVQXHYTUVOIYRNUUOUVRUUKYLXQBVTSWAWFYPUUMUUDXHUVKYMNU
      UOUUIUUKYLXNBWHSYOYMYJNYFYOYMXOLYOYMTXPUVEUVFXPYMUVDXOLYLOWJWKUJWLVJWMWNY
      FUUPYHYJMUUTYHUUPYJXOWQWPRZWRWSXBUVSUVIXCXDXEXF $.
      $( [24-Feb-2014] $)
  $}

  ${
    $d p K $.  $d p M $.  $d p N $.
    $( If ` K ` is relatively prime to ` M ` and to ` N ` , it is also
       relatively prime to their product.  (Contributed by Mario Carneiro,
       24-Feb-2014.) $)
    rpmul $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                  ( ( ( K gcd M ) = 1 /\ ( K gcd N ) = 1 ) ->
                    ( K gcd ( M x. N ) ) = 1 ) ) $=
      ( vp cz wcel w3a cgcd co c1 wceq wa cdivides wbr cprime wn wo syl2anc syl
      cc0 cmul c2 cuz cfv cv wrex nprmdvds1 adantl ad2antrl cn0 simpll1 simpll2
      prmz simpll3 zmulcl nn0z simprr gcddvds simprd dvdstr imp syl32anc simprl
      gcdcl euclemma syl3anc mpbid simpld dvdsgcd simplrl breq2d sylibd simplrr
      wb mpand jaod mpd expr mtod nrexdv exprmfct nsyl simpl1 simpl2 simpl3 wne
      wi cn ax-1ne0 gcd0val neeqtrri necomi df-ne mpbi zcn mul0or anbi2d syl6bb
      cc andi oveq12 eqeq1d syl5ibcom sylbid mtoi gcdn0cl syl21anc elnn1uz2 ord
      sylib mt3d ex ) AEFZBEFZCEFZGZABHIZJKZACHIZJKZLZABCUAIZHIZJKZXPYALZYDYCUB
      UCUDFZYEDUEZYCMNZDOUFYFYEYHDOYEYGOFZLYHYGJMNZYIYJPYEYGUGUHYEYIYHYJYEYIYHL
      ZLZYGBMNZYGCMNZQZYJYLYGYBMNZYOYLYGEFZYCEFZYBEFZYHYCYBMNZYPYIYQYEYHYGUMUIZ
      YLYCUJFZYRYLXMYSUUBXMXNXOYAYKUKZYLXNXOYSXMXNXOYAYKULZXMXNXOYAYKUNZBCUOZRZ
      AYBVDRYCUPSZUUGYEYIYHUQZYLYCAMNZYTYLXMYSUUJYTLUUCUUGAYBURRZUSYQYRYSGYHYTL
      YPYGYCYBUTVAVBYLYIXNXOYPYOVNYEYIYHVCUUDUUEYGBCVEVFVGYLYMYJYNYLYMYGXQMNZYJ
      YLYGAMNZYMUULYLYQYRXMYHUUJUUMUUAUUHUUCUUIYLUUJYTUUKVHYQYRXMGYHUUJLUUMYGYC
      AUTVAVBZYLYQXMXNUUMYMLUULWGUUAUUCUUDYGABVIVFVOYLXQJYGMXPXRXTYKVJVKVLYLYNY
      GXSMNZYJYLUUMYNUUOUUNYLYQXMXOUUMYNLUUOWGUUAUUCUUEYGACVIVFVOYLXSJYGMXPXRXT
      YKVMVKVLVPVQVRVSVTYCDWAWBYEYDYFYEYCWHFZYDYFQYEXMYSATKZYBTKZLZPUUPXMXNXOYA
      WCYEXNXOYSXMXNXOYAWDZXMXNXOYAWEZUUFRYEUUSTTHIZJKZUVBJWFUVCPJUVBJTUVBWIWJW
      KWLUVBJWMWNYEUUSUUQBTKZLZUUQCTKZLZQZUVCYEUUSUUQUVDUVFQZLUVHYEUURUVIUUQYEB
      WSFZCWSFZUURUVIVNYEXNUVJUUTBWOSYEXOUVKUVACWOSBCWPRWQUUQUVDUVFWTWRYEUVEUVC
      UVGYEXRUVEUVCXPXRXTVCUVEXQUVBJATBTHXAXBXCYEXTUVGUVCXPXRXTUQUVGXSUVBJATCTH
      XAXBXCVPXDXEAYBXFXGYCXHXJXIXKXL $.
      $( [24-Feb-2014] $)
  $}

