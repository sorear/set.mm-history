$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Decimal arithmetic (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    dec2dvds1.1 $e |- A e. NN0 $.
    $( Divisibility by two is obvious in base 4.  (Contributed by Mario
       Carneiro, 18-Feb-2014.) $)
    dec2dvds1 $p |- -. 2 || ( ( 4 x. A ) + 1 ) $=
      ( c2 c4 cmul co caddc 2nn 2nn0 nn0mulcli 1nn decbin0 oveq1i 1lt2 ndvdsi
      c1 ) CDAEFZPGFCAEFZPHCAIBJKQCREFPGABLMNO $.
      $( [18-Feb-2014] $)

    $( Divisibility by two is obvious in base 4.  (Contributed by Mario
       Carneiro, 18-Feb-2014.) $)
    dec2dvds3 $p |- -. 2 || ( ( 4 x. A ) + 3 ) $=
      ( c2 c4 cmul co c3 caddc c1 2nn 2nn0 nn0mulcli 1nn0 nn0addcli 1nn decbin3
      1lt2 ndvdsi ) CDAEFGHFCAEFZIHFIJSICAKBLMNOABPQR $.
      $( [18-Feb-2014] $)
  $}

  ${
    dec2nprm.1 $e |- A e. NN $.
    $( Divisibility by two is obvious in base 4. $)
    dec2nprm $p |- -. ( ( 4 x. A ) + 2 ) e. Prime $=
      ( c2 cmul co c1 caddc c4 2nn wcel nnmulcli 1nn nnaddcl mp2an 1lt2 cc0 clt
      cn ax-1cn wbr addid2i nngt0i 0re 1re ltadd1i mpbi eqbrtrri nnnn0i decbin2
      nnrei nprmi ) CCADEZFGEZHADECGEIULRJFRJUMRJCAIBKZLULFMNOPFGEZFUMQFSUAPULQ
      TUOUMQTULUNUBPULFUCULUNUJUDUEUFUGAABUHUIUK $.
      $( [18-Feb-2014] $)
  $}

  ${
    modxai.1 $e |- N e. NN $.
    modxai.2 $e |- A e. NN $.
    modxai.3 $e |- B e. NN0 $.
    modxai.4 $e |- D e. ZZ $.
    modxai.5 $e |- K e. NN0 $.
    modxai.6 $e |- M e. NN0 $.
    ${
      modxai.7 $e |- C e. NN0 $.
      modxai.8 $e |- L e. NN0 $.
      modxai.9 $e |- E = ( B + C ) $.
      modxai.10 $e |- ( ( D x. N ) + M ) = ( K x. L ) $.
      modxai.11 $e |- ( ( A ^ B ) mod N ) = ( K mod N ) $.
      modxai.12 $e |- ( ( A ^ C ) mod N ) = ( L mod N ) $.
      $( Add exponents in a power mod calculation.  (Contributed by Mario
         Carneiro, 21-Feb-2014.) $)
      modxai $p |- ( ( A ^ E ) mod N ) = ( M mod N ) $=
        ( cexp co cmo cmul caddc oveq2i wcel cn0 wceq nncni expadd mp3an oveq1i
        cc eqtri cr wa cz crp nnexpcl mp2an nnrei nn0rei pm3.2i nnzi nnrp ax-mp
        modmul1 nn0cni mulcomi nn0zi zcn mulcli addcomi 3eqtr2i 3eqtri modcyc
        cn ) AEUBUCZIUDUCABUBUCZACUBUCZUEUCZIUDUCZHDIUEUCZUFUCZIUDUCZHIUDUCZVTW
        CIUDVTABCUFUCZUBUCZWCEWIAUBRUGAUOUHBUIUHZCUIUHZWJWCUJAKUKLPABCULUMUPUNW
        DWBFUEUCZIUDUCZGFUEUCZIUDUCZWGWDFWBUEUCZIUDUCZWNWAUQUHZFUQUHZURWBUSUHZI
        UTUHZURWAIUDUCFIUDUCUJWDWRUJWSWTWAAVSUHZWKWAVSUHKLABVAVBVCFNVDVEXAXBWBX
        CWLWBVSUHKPACVAVBZVFIVSUHXBJIVGVHZVETWAFWBIVIUMWQWMIUDFWBFNVJZWBXDUKVKU
        NUPWBUQUHZGUQUHZURFUSUHZXBURWBIUDUCGIUDUCUJWNWPUJXGXHWBXDVCGQVDVEXIXBFN
        VLXEVEUAWBGFIVIUMWOWFIUDWOFGUEUCWEHUFUCWFGFGQVJXFVKSWEHDIDUSUHZDUOUHMDV
        MVHIJUKVNHOVJVOVPUNVQHUQUHXBXJWGWHUJHOVDXEMHIDVRUMVQ $.
        $( [21-Feb-2014] $)
    $}

    ${
      mod2xi.7 $e |- E = ( 2 x. B ) $.
      mod2xi.8 $e |- ( ( D x. N ) + M ) = ( K x. K ) $.
      mod2xi.9 $e |- ( ( A ^ B ) mod N ) = ( K mod N ) $.
      $( Double exponents in a power mod calculation.  (Contributed by Mario
         Carneiro, 21-Feb-2014.) $)
      mod2xi $p |- ( ( A ^ E ) mod N ) = ( M mod N ) $=
        ( c2 cmul co caddc nn0cni 2timesi eqtri modxai ) ABBCDEEFGHIJKLMJLDQBRS
        BBTSNBBJUAUBUCOPPUD $.
        $( [21-Feb-2014] $)
    $}

    ${
      modxp1i.7 $e |- E = ( B + 1 ) $.
      modxp1i.8 $e |- ( ( D x. N ) + M ) = ( K x. A ) $.
      modxp1i.9 $e |- ( ( A ^ B ) mod N ) = ( K mod N ) $.
      $( Add one to an exponent in a power mod calculation.  (Contributed by
         Mario Carneiro, 21-Feb-2014.) $)
      modxp1i $p |- ( ( A ^ E ) mod N ) = ( M mod N ) $=
        ( c1 1nn0 nnnn0i cexp co cmo wcel wceq nncni exp1 ax-mp oveq1i modxai
        cc ) ABQCDEAFGHIJKLMRAISNOPAQTUAZAGUBAUJUCUKAUDAIUEAUFUGUHUI $.
        $( [21-Feb-2014] $)
    $}
  $}

  ${
    mod2xnegi.1 $e |- A e. NN $.
    mod2xnegi.2 $e |- B e. NN0 $.
    mod2xnegi.3 $e |- D e. ZZ $.
    mod2xnegi.4 $e |- K e. NN $.
    mod2xnegi.5 $e |- M e. NN0 $.
    mod2xnegi.6 $e |- L e. NN0 $.
    mod2xnegi.7 $e |- E = ( 2 x. B ) $.
    mod2xnegi.8 $e |- N = ( L + K ) $.
    mod2xnegi.9 $e |- ( ( D x. N ) + M ) = ( K x. K ) $.
    mod2xnegi.10 $e |- ( ( A ^ B ) mod N ) = ( L mod N ) $.
    $( Version of ~ mod2xi with a negaive mod value.  (Contributed by Mario
       Carneiro, 21-Feb-2014.) $)
    mod2xnegi $p |- ( ( A ^ E ) mod N ) = ( M mod N ) $=
      ( caddc co cmin cn wcel nn0nnaddcl mp2an eqeltri cz nnzi zaddcl nn0addcli
      cn0 nnnn0i nn0zi zsubcl cmul nncni zcn ax-mp addcli subdiri oveq1i mulcli
      nn0cni addsubi oveq2i adddii oveq12i adddiri addassi eqtr2i mulcomi mp4an
      cc wceq mulsub eqcomi subadd2i mpbir eqtr3i 3eqtr3i 3eqtr2i mod2xi ) ABHC
      STZEESTZUATZDFGHHFESTZUBPFUKUCEUBUCWFUBUCNLFEUDUEUFZIJWCUGUCZWDUGUCWEUGUC
      HUGUCCUGUCZWHHWGUHKHCUIUEWDEEELULZWJUJUMWCWDUNUENMOWEHUOTZGSTWCHUOTZWDHUO
      TZUATZGSTWLGSTZWMUATZFFUOTZWKWNGSWCWDHHCHWGUPZWICVMUCKCUQURZUSZEEELUPZXAU
      SZWRUTVAWLGWMWCHWTWRVBGMVCZWDHXBWRVBVDHHUOTZCHUOTZGSTZSTZHWDUOTZUATXDEEUO
      TZSTZHEUOTZXKSTZUATZWPWQXGXJXHXLUAXFXIXDSQVEHEEWRXAXAVFVGXGWOXHWMUAWOXDXE
      STZGSTXGWLXNGSHCHWRWSWRVHVAXDXEGHHWRWRVBCHWSWRVBXCVIVJHWDWRXBVKVGHEUATZXO
      UOTZXMWQHVMUCZEVMUCZXQXRXPXMVNWRXAWRXAHEHEVOVLXOFXOFUOXOFVNWFHVNHWFPVPHEF
      WRXAFNVCVQVRZXSVGVSVTWARWB $.
      $( [21-Feb-2014] $)
  $}

  ${
    modsubi.1 $e |- N e. NN $.
    modsubi.2 $e |- A e. NN $.
    modsubi.3 $e |- B e. NN0 $.
    modsubi.4 $e |- M e. NN0 $.
    modsubi.5 $e |- K = ( M + B ) $.
    modsubi.6 $e |- ( A mod N ) = ( K mod N ) $.
    $( Subtract from within a mod calculation.  (Contributed by Mario Carneiro,
       18-Feb-2014.) $)
    modsubi $p |- ( ( A - B ) mod N ) = ( M mod N ) $=
      ( caddc co cmo cmin cr wcel wa wceq nn0rei nnrei nn0addcli eqeltri pm3.2i
      cneg crp renegcli cn nnrp ax-mp modadd1 mp3an nncni nn0cni negsubi oveq1i
      recni eqcomi subadd2i mpbir eqtri 3eqtr3i ) ABUEZLMZENMZCVCLMZENMZABOMZEN
      MDENMAPQZCPQZRVCPQZEUFQZRAENMCENMSVEVGSVIVJAGUACDBLMZPJVMDBIHUBTUCZUDVKVL
      BBHTUGEUHQVLFEUIUJUDKACVCEUKULVDVHENABAGUMBHUNZUOUPVFDENVFCBOMZDCBCVNUQZV
      OUOVPDSVMCSCVMJURCBDVQVODIUNUSUTVAUPVB $.
      $( [18-Feb-2014] $)
  $}

  ${
    gcdi.1 $e |- K e. NN0 $.
    gcdi.2 $e |- R e. NN0 $.
    ${
      gcdi.3 $e |- N e. NN0 $.
      gcdi.4 $e |- M = ( ( K x. N ) + R ) $.
      gcdi.5 $e |- ( N gcd R ) = G $.
      $( Calculate a GCD via Euclid's algorithm.  (Contributed by Mario
         Carneiro, 19-Feb-2014.) $)
      gcdi $p |- ( M gcd N ) = G $=
        ( cgcd co cmul caddc nn0cni eqtri cz wcel wceq nn0zi addcomi oveq2i cn0
        nn0mulcli nn0addcli eqeltri gcdcom mp2an gcdaddm mp3an 3eqtr4i ) DEKLZE
        AKLZBEDKLZEACEMLZNLZKLZULUMDUPEKDUOANLZUPIUOAUOCEFHUDZOAGOUAPUBDQREQRZU
        LUNSDDURUCIUOAUSGUEUFTEHTZDEUGUHCQRUTAQRUMUQSCFTVAAGTCEAUIUJUKJP $.
        $( [19-Feb-2014] $)
    $}

    ${
      gcdmodi.3 $e |- N e. NN $.
      gcdmodi.4 $e |- ( K mod N ) = ( R mod N ) $.
      gcdmodi.5 $e |- ( N gcd R ) = G $.
      $( Calculate a GCD via Euclid's algorithm.  (Contributed by Mario
         Carneiro, 19-Feb-2014.) $)
      gcdmodi $p |- ( K gcd N ) = G $=
        ( cgcd co cmo oveq1i cz wcel cn wceq nn0zi modgcd mp2an 3eqtr3i gcdcom
        nnzi 3eqtri ) CDJKZADJKZDAJKZBCDLKZDJKZADLKZDJKZUEUFUHUJDJHMCNODPOZUIUE
        QCERGCDSTANOZULUKUFQAFRZGADSTUAUMDNOUFUGQUNDGUCADUBTIUD $.
        $( [19-Feb-2014] $)
    $}
  $}

  ${
    decexp2.1 $e |- M e. NN0 $.
    decexp2.2 $e |- ( M + 2 ) = N $.
    $( Calculate a power of two.  (Contributed by Mario Carneiro,
       19-Feb-2014.) $)
    decexp2 $p |- ( ( 4 x. ( 2 ^ M ) ) + 0 ) = ( 2 ^ N ) $=
      ( c2 cexp co cmul caddc cn0 wcel mp2an c1 wceq expp1 nn0cni ax-1cn oveq2i
      2cn mulcomi c4 cc0 2nn0 nn0expcl dec0u cc peano2nn0 addassi df-2 3eqtr2ri
      ax-mp decbin0 mulcli eqtr2i oveq1i 3eqtri 3eqtr4ri eqtri ) UAEAFGZHGZUBIG
      UTEBFGZUSEJKAJKZUSJKUCCEAUDLZUEEAMIGZMIGZFGZEVDFGZEHGZVAUTEUFKZVDJKZVFVHN
      SVBVJCAUGUKEVDOLBVEEFVEAMMIGZIGAEIGBAMMACPQQUHEVKAIUIRDUJRUTEEUSHGZHGVLEH
      GVHUSVCULEVLSEUSSUSVCPZUMTVLVGEHVGUSEHGZVLVIVBVGVNNSCEAOLUSEVMSTUNUOUPUQU
      R $.
      $( [19-Feb-2014] $)
  $}

