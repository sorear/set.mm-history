$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Pythagorean Triples
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( If three numbers are coprime, and the square of one is the product of the
     other two, then there is a formula for the other two in terms of ` gcd `
     and square.  (Contributed by Scott Fenton, 02-Apr-2014.) $)
  coprimeprodsq $p |- ( ( ( A e. NN0 /\ B e. ZZ /\ C e. NN0 ) /\
    ( ( A gcd B ) gcd C ) = 1 ) -> ( ( C ^ 2 ) = ( A x. B ) ->
      A = ( ( A gcd C ) ^ 2 ) ) ) $=
    ( cn0 wcel cz cgcd co c1 wceq cmul cc 3ad2ant1 nn0cn syl2anc mulgcd syl3anc
    nn0z syl 3eqtr3d w3a c2 cexp gcdcl syl2an 3adant2 3syl simp13 mulcom simpl3
    sqval wa eqeq1d biimp3a oveq12d simp11 simp12 oveq2d mulgcdr ancoms 3adant1
    sylan cabs 3ad2ant3 gcdid oveq1d simp2 gcdabs1 eqtrd gcdass 3eqtr4d biimpar
    cfv gcdcom 3adant3 mulid1 3eqtrrd 3expia ) ADEZBFEZCDEZUAZABGHCGHZIJZCUBUCH
    ZABKHZJZAACGHZUBUCHZJWBWDWGUAZWIWHWHKHZAWHCBGHZGHZKHZAWJWHDEZWHLEWIWKJWBWDW
    OWGVSWAWOVTVSAFEZCFEZWOWAARZCRZACUDUEUFMZWHNWHUKUGWJAWHKHZCWHKHZGHZXAAWLKHZ
    GHZWKWNWJXBXDXAGWJCAKHZCCKHZGHZACKHZWFGHZXBXDWJXFXIXGWFGWJCLEZALEZXFXIJWJWA
    XKVSVTWAWDWGUHZCNZSWBWDXLWGVSVTXLWAANMMZCAUIOWBWDWGXGWFJWBWDULZWEXGWFXPWAXK
    WEXGJVSVTWAWDUJXNCUKUGUMUNUOWJWAWPWQXHXBJXMWJVSWPVSVTWAWDWGUPZWRSZWJWAWQXMW
    SSZCACPQWJVSWQVTXJXDJXQXSVSVTWAWDWGUQACBPQTURWJWPWQWOXCWKJXRXSWTACWHUSQWJVS
    WHFEZWLFEZXEWNJXQWJWOXTWTWHRSWJWLDEZYAWBWDYBWGVTWAYBVSWAVTYBWAWQVTYBWSCBUDV
    BUTVAZMWLRZSAWHWLPQTWJWNAIKHZAWBWDWNYEJWGXPWMIAKWBWMIJWDWBWMWCIWBACWLGHZGHZ
    ABCGHZGHZWMWCWBYFYHAGWBCCGHZBGHZWLYFYHWBYKCVCVMZBGHZWLWBYJYLBGWBWQYJYLJWAVS
    WQVTWSVDZCVESVFWBWQVTYMWLJYNVSVTWAVGZBCVHOVIWBWQWQVTYKYFJYNYNYOBCCVJQWBWQVT
    WLYHJYNYOCBVNOTURWBWPWQYAWMYGJVSVTWPWAWRMZYNWBYBYAYCYDSWLCAVJQWBWPVTWQWCYIJ
    YPYOYNCBAVJQVKUMVLURVOWJXLYEAJXOAVPSVIVQVR $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( If three numbers are coprime, and the square of one is the product of the
     other two, then there is a formula for the other two in terms of ` gcd `
     and square.  (Contributed by Scott Fenton, 17-Apr-2014.) $)
  coprimeprodsq2 $p |- ( ( ( A e. ZZ /\ B e. NN0 /\ C e. NN0 ) /\
    ( ( A gcd B ) gcd C ) = 1 ) -> ( ( C ^ 2 ) = ( A x. B ) ->
      B = ( ( B gcd C ) ^ 2 ) ) ) $=
    ( cz wcel cn0 w3a cgcd co c1 wceq wa c2 cexp cmul zcn nn0cn mulcom 3adant3
    cc syl2an adantr eqeq2d wi simpl2 simpl1 simpl3 gcdcom oveq1d eqeq1d sylan2
    wb nn0z biimpa coprimeprodsq syl31anc sylbid ) ADEZBFEZCFEZGZABHIZCHIZJKZLZ
    CMNIZABOIZKVFBAOIZKZBBCHIMNIKZVEVGVHVFVAVGVHKZVDURUSVKUTURATEBTEVKUSAPBQABR
    UASUBUCVEUSURUTBAHIZCHIZJKZVIVJUDURUSUTVDUEURUSUTVDUFURUSUTVDUGVAVDVNURUSVD
    VNULZUTUSURBDEZVOBUMURVPLZVCVMJVQVBVLCHABUHUIUJUKSUNBACUOUPUQ $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  ${
    $d A a b c $.  $d B a b c $.
    $( The sum of two odds is even.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    opoe $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ -. 2 || B ) ) ->
       2 || ( A + B ) ) $=
      ( va vb cz wcel c2 cdivides wbr wn caddc co wa cmul c1 wceq cc 2cn ax-1cn
      wrex cv odd2np1 bi2anan9 reeanv 2z peano2zdi dvdsmul1 sylancr addcl adddi
      zaddcl zcn mp3an13 syl mp3an1 oveq1d eqtrd df-2 eqtri oveq2i syl6eq mulcl
      mulid1i mpan add4 mpanr12 syl2an oveq12 breq2d syl5ibcom rexlimivv sylbir
      breqtrd syl6bi imp an4s ) AEFZBEFZGAHIJZGBHIJZGABKLZHIZVQVRMZVSVTMZWBWCWD
      GCUAZNLZOKLZAPZCETZGDUAZNLZOKLZBPZDETZMZWBVQVSWIVRVTWNCAUBDBUBUCWOWHWMMZD
      ETCETWBWHWMCDEEUDWPWBCDEEWEEFZWJEFZMZGWGWLKLZHIWPWBWSGGWEWJKLZOKLZNLZWTHW
      SGEFXBEFGXCHIUEWSXAWEWJUKUFGXBUGUHWQWEQFZWJQFZXCWTPWRWEULWJULXDXEMZXCWFWK
      KLZOOKLZKLZWTXFXCXGGONLZKLZXIXFXCGXANLZXJKLZXKXFXAQFZXCXMPZWEWJUIGQFZXNOQ
      FZXORSGXAOUJUMUNXFXLXGXJKXPXDXEXLXGPRGWEWJUJUOUPUQXJXHXGKXJGXHGRVCURUSUTV
      AXDWFQFZWKQFZXIWTPZXEXPXDXRRGWEVBVDXPXEXSRGWJVBVDXRXSMXQXQXTSSWFWKOOVEVFV
      GUQVGVMWPWTWAGHWGAWLBKVHVIVJVKVLVNVOVP $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The difference of two odds is even.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    omoe $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ -. 2 || B ) ) ->
       2 || ( A - B ) ) $=
      ( va vb cz wcel c2 cdivides wbr wn cmin co wa cv cmul c1 wceq wrex cc 2cn
      caddc odd2np1 bi2anan9 reeanv 2z zsubcl dvdsmul1 sylancr zcn mulcl ax-1cn
      mpan pnpcan2 mp3an3 syl2an mp3an1 eqtr4d breqtrrd oveq12 breq2d syl5ibcom
      subdi rexlimivv sylbir syl6bi imp an4s ) AEFZBEFZGAHIJZGBHIJZGABKLZHIZVHV
      IMZVJVKMZVMVNVOGCNZOLZPUALZAQZCERZGDNZOLZPUALZBQZDERZMZVMVHVJVTVIVKWECAUB
      DBUBUCWFVSWDMZDERCERVMVSWDCDEEUDWGVMCDEEVPEFZWAEFZMZGVRWCKLZHIWGVMWJGGVPW
      AKLZOLZWKHWJGEFWLEFGWMHIUEVPWAUFGWLUGUHWHVPSFZWASFZWKWMQWIVPUIWAUIWNWOMWK
      VQWBKLZWMWNVQSFZWBSFZWKWPQZWOGSFZWNWQTGVPUJULWTWOWRTGWAUJULWQWRPSFWSUKVQW
      BPUMUNUOWTWNWOWMWPQTGVPWAVBUPUQUOURWGWKVLGHVRAWCBKUSUTVAVCVDVEVFVG $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The sum of an odd and an even is odd.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    opeo $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ 2 || B ) ) ->
      -. 2 || ( A + B ) ) $=
      ( vc va vb cz wcel c2 cdivides wbr wa caddc co cmul c1 wceq wrex mpan 2cn
      cc wn cv odd2np1 wb 2z divides bi2anan9 reeanv zaddcl adddi mp3an1 oveq1d
      mulcl ax-1cn add32 mp3an3 syl2an mulcom adantl oveq2d 3eqtrd oveq2 eqeq1d
      zcn rcla4ev syl2anc oveq12 eqeq2d rexbidv syl5ibcom rexlimivv sylbir an4s
      syl6bi imp ad2ant2r syl mpbird ) AFGZHAIJUAZKBFGZHBIJZKKZHABLMZIJUAZHCUBZ
      NMZOLMZWDPZCFQZVSWAVTWBWJVSWAKZVTWBKZWJWKWLHDUBZNMZOLMZAPZDFQZEUBZHNMZBPZ
      EFQZKZWJVSVTWQWAWBXADAUCHFGWAWBXAUDUEEHBUFRUGXBWPWTKZEFQDFQWJWPWTDEFFUHXC
      WJDEFFWMFGZWRFGZKZWHWOWSLMZPZCFQZXCWJXFWMWRLMZFGHXJNMZOLMZXGPZXIWMWRUIXDW
      MTGZWRTGZXMXEWMVDWRVDXNXOKZXLWNHWRNMZLMZOLMZWOXQLMZXGXPXKXROLHTGZXNXOXKXR
      PSHWMWRUJUKULXNWNTGZXQTGZXSXTPZXOYAXNYBSHWMUMRYAXOYCSHWRUMRYBYCOTGYDUNWNX
      QOUOUPUQXPXQWSWOLXOXQWSPZXNYAXOYESHWRURRUSUTVAUQXHXMCXJFWFXJPZWHXLXGYFWGX
      KOLWFXJHNVBULVCVEVFXCXHWICFXCXGWDWHWOAWSBLVGVHVIVJVKVLVNVOVMWCWDFGZWEWJUD
      VSWAYGVTWBABUIVPCWDUCVQVR $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The difference of an odd and an even is odd.  (Contributed by Scott
       Fenton, 7-Apr-2014.) $)
    omeo $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ 2 || B ) ) ->
      -. 2 || ( A - B ) ) $=
      ( vc va vb cz wcel c2 cdivides wa cmin co cmul c1 caddc wceq wrex mpan cc
      2cn wbr wn cv odd2np1 wb divides bi2anan9 reeanv zsubcl zcn mp3an1 oveq1d
      subdi mulcl ax-1cn addsub mp3an2 syl2an mulcom oveq2d adantl oveq2 eqeq1d
      3eqtr2d rcla4ev syl2anc oveq12 eqeq2d rexbidv syl5ibcom sylbir syl6bi imp
      2z rexlimivv an4s ad2ant2r syl mpbird ) AFGZHAIUAUBZJBFGZHBIUAZJJZHABKLZI
      UAUBZHCUCZMLZNOLZWEPZCFQZVTWBWAWCWKVTWBJZWAWCJZWKWLWMHDUCZMLZNOLZAPZDFQZE
      UCZHMLZBPZEFQZJZWKVTWAWRWBWCXBDAUDHFGWBWCXBUEVNEHBUFRUGXCWQXAJZEFQDFQWKWQ
      XADEFFUHXDWKDEFFWNFGZWSFGZJZWIWPWTKLZPZCFQZXDWKXGWNWSKLZFGHXKMLZNOLZXHPZX
      JWNWSUIXEWNSGZWSSGZXNXFWNUJWSUJXOXPJZXMWOHWSMLZKLZNOLZWPXRKLZXHXQXLXSNOHS
      GZXOXPXLXSPTHWNWSUMUKULXOWOSGZXRSGZYAXTPZXPYBXOYCTHWNUNRYBXPYDTHWSUNRYCNS
      GYDYEUOWONXRUPUQURXPYAXHPXOXPXRWTWPKYBXPXRWTPTHWSUSRUTVAVDURXIXNCXKFWGXKP
      ZWIXMXHYFWHXLNOWGXKHMVBULVCVEVFXDXIWJCFXDXHWEWIWPAWTBKVGVHVIVJVOVKVLVMVPW
      DWEFGZWFWKUEVTWBYGWAWCABUIVQCWEUDVRVS $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)
  $}

  ${
    $d A n m k $.  $d B n m k $.  $d C n m k $.
    $( Lemma for ~ pythagtrip .  Prove a weaker version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem1 $p |- (
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) $=
      ( c2 cexp co cmin cmul wceq caddc cn wcel cc sqcl syl 2cn c4 cv w3a wa wi
      wrex nncn adantl mulcl syl2anr sylancr subcl syl2anc adantr add32 syl3anc
      ancoms subadd23 sqmul sq2 a1i oveq12d eqtrd oveq1d 4re recni 2p2e4 oveq1i
      subaddrii subdir mp3an12 syl5reqr oveq2d binom2sub binom2 3eqtr4d 3adant3
      simp3 3ad2ant2 3ad2ant1 adddi eqtr4d addcl syl3an oveq1 oveqan12d eqeq12d
      3ad2ant3 syl5ibrcom 3expa rexlimdva rexlimivv ) ADUAZEUAZGHIZFUAZGHIZJIZK
      IZLZBWLGWMWOKIZKIZKIZLZCWLWNWPMIZKIZLZUBZDNUEAGHIZBGHIZMIZCGHIZLZFENNWONO
      ZWMNOZUCXGXLDNXMXNWLNOZXGXLUDXMXNXOUBXLXGWRGHIZXBGHIZMIZXEGHIZLZXMWOPOZXN
      WMPOZXOWLPOZXTWOUFWMUFWLUFYAYBYCUBZWLGHIZWQGHIZXAGHIZMIZKIZYEXDGHIZKIZXRX
      SYDYHYJYEKYAYBYHYJLYCYAYBUCZWNGHIZGWNWPKIZKIZJIZWPGHIZMIZYGMIZYMYOMIZYQMI
      ZYHYJYLYSYPYGMIZYQMIZUUAYLYPPOZYQPOZYGPOZYSUUCLYLYMPOZYOPOZUUDYLWNPOZUUGY
      BUUIYAWMQZUGWNQRZYLGPOZYNPOZUUHSYBUUIWPPOZUUMYAUUJWOQZWNWPUHUIZGYNUHUJZYM
      YOUKULYLUUNUUEYAUUNYBUUOUMWPQRYLXAPOZUUFYLUULWTPOZUURSYBYAUUSWMWOUHUPZGWT
      UHZUJXAQZRZYPYQYGUNUOYLUUBYTYQMYLUUBYMYGYOJIZMIZYTYLUUGUUHUUFUUBUVELUUKUU
      QUVCYMYOYGUQUOYLUVDYOYMMYLUVDTYNKIZYOJIZYOYLYGUVFYOJYLYGGGHIZWTGHIZKIZUVF
      YLUULUUSYGUVJLSUUTGWTURUJYLUVHTUVIYNKUVHTLYLUSUTYBYAUVIYNLWMWOURUPVAVBVCY
      LYOTGJIZYNKIZUVGUVKGYNKTGGTVDVEZSSVFVHVGYLUUMUVLUVGLZUUPTPOUULUUMUVNUVMST
      GYNVIVJRVKVBVLVBVCVBYLYFYRYGMYBUUIUUNYFYRLYAUUJUUOWNWPVMUIVCYBUUIUUNYJUUA
      LYAUUJUUOWNWPVNUIVOVPVLYDXRYEYFKIZYEYGKIZMIZYIYDXPUVOXQUVPMYDYCWQPOZXPUVO
      LYAYBYCVQZYDUUIUUNUVRYBYAUUIYCUUJVRZYAYBUUNYCUUOVSZWNWPUKULZWLWQURULYDYCU
      URXQUVPLUVSYDUULUUSUURSYAYBUUSYCUUTVPUVAUJZWLXAURULVAYDYEPOZYFPOZUUFYIUVQ
      LYCYAUWDYBWLQWGYDUVRUWEUWBWQQRYDUURUUFUWCUVBRYEYFYGVTUOWAYDYCXDPOZXSYKLUV
      SYDUUIUUNUWFUVTUWAWNWPWBULWLXDURULVOWCXGXJXRXKXSWSXCXJXRLXFWSXCXHXPXIXQMA
      WRGHWDBXBGHWDWEVPXFWSXKXSLXCCXEGHWDWGWFWHWIWJWK $.
      $( [19-Apr-2014] $) $( [28-Mar-2014] $)

    $( Lemma for ~ pythagtrip .  Prove the full version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem2 $p |- ( ( A e. NN /\ B e. NN ) -> (
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) ) $=
      ( cn wcel wa cv c2 cexp co cmul wceq caddc wrex wo cvv cc cpr cmin w3a wb
      ovex preq12bg mpanr12 anbi1d andir df-3an orbi12i bitr4i rexbidv 2rexbidv
      syl6bb r19.43 2rexbii rexbii 3bitri pythagtriplem1 a1i 3ancoma sylbi nncn
      wi sqcl syl addcom syl2an eqeq1d syl5ibr jaod sylbid ) AGHZBGHZIZABUADJZE
      JZKLMZFJZKLMZUBMZNMZVQKVRVTNMNMZNMZUAOZCVQVSWAPMNMOZIZDGQZEGQFGQZAWCOZBWE
      OZWGUCZDGQZEGQZFGQZAWEOZBWCOZWGUCZDGQZEGQZFGQZRZAKLMZBKLMZPMZCKLMZOZVPWJW
      MWSRZDGQZEGQFGQZXCVPWIXJFEGGVPWHXIDGVPWHWKWLIZWQWRIZRZWGIZXIVPWFXNWGVPWCS
      HWESHWFXNUDVQWBNUEVQWDNUEABWCWEGGSSUFUGUHXOXLWGIZXMWGIZRXIXLXMWGUIWMXPWSX
      QWKWLWGUJWQWRWGUJUKULUOUMUNXKWNWTRZEGQZFGQWOXARZFGQXCXJXRFEGGWMWSDGUPUQXS
      XTFGWNWTEGUPURWOXAFGUPUSUOVPWPXHXBWPXHVEVPABCDEFUTVAXBXHVPXEXDPMZXGOZXBWR
      WQWGUCZDGQZEGQFGQYBWTYDFEGGWSYCDGWQWRWGVBURUQBACDEFUTVCVPXFYAXGVNXDTHZXET
      HZXFYAOVOVNATHYEAVDAVFVGVOBTHYFBVDBVFVGXDXEVHVIVJVKVLVM $.
      $( [19-Apr-2014] $) $( [29-Mar-2014] $)

  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C ` and ` B ` are relatively prime
       under some conditions.  (Contributed by Scott Fenton, 8-Apr-2014.) $)
    pythagtriplem3 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( B gcd C ) = 1 ) $=
      ( cn wcel w3a c2 cexp co wceq cgcd c1 wbr wa nnz syl 3ad2ant1 syl2anc cc0
      cz caddc cdivides oveq2 adantl zsqcl 3ad2ant2 gcdadd gcdcom eqtr3d adantr
      wn simpl2 simpl3 sqgcd simpl1 3eqtr4d 3adant3 simp3l oveq1d eqtrd cle cn0
      cr wb 3ad2ant3 gcdcl nn0re nn0ge0 1re 0re lt01 ltleii sq11 mpanr12 mpbid
      ) ADEZBDEZCDEZFZAGHIZBGHIZUAIZCGHIZJZABKIZLJZGAUBMUKZNZFZBCKIZGHIZLGHIZJZ
      WJLJZWIWKWEGHIZWLVSWDWKWOJWHVSWDNZWAWCKIZVTWAKIZWKWOWPWAWBKIZWQWRWDWSWQJV
      SWBWCWAKUCUDVSWSWRJWDVSWAVTKIZWSWRVSWATEZVTTEZWTWSJVQVPXAVRVQBTEZXABOZBUE
      PUFZVPVQXBVRVPATEXBAOAUEPQZWAVTUGRVSXAXBWTWRJXEXFWAVTUHRUIUJUIWPVQVRWKWQJ
      VPVQVRWDULZVPVQVRWDUMBCUNRWPVPVQWOWRJVPVQVRWDUOXGABUNRUPUQWIWELGHVSWDWFWG
      URUSUTWIWJVCEZSWJVAMZWMWNVDZVSWDXHWHVSWJVBEZXHVSXCCTEZXKVQVPXCVRXDUFVRVPX
      LVQCOVEBCVFRZWJVGPQVSWDXIWHVSXKXIXMWJVHPQXHXINLVCESLVAMXJVISLVJVIVKVLWJLV
      MVNRVO $.
      $( [19-Apr-2014] $) $( [8-Apr-2014] $)
  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C - B ` and ` C + B ` are
       relatively prime.  (Contributed by Scott Fenton, 12-Apr-2014.) $)
    pythagtriplem4 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( ( C - B ) gcd ( C + B ) ) = 1 ) $=
      ( cn wcel c2 co caddc wceq cgcd c1 cdivides wbr wa cmin cz syl2anc syl cc
      cmul w3a cexp wn simp3r nnz zsubcl syl2anr 3adant1 3ad2ant1 simp13 simp12
      wo nnaddcl gcddvds simprd breq1 biimpd mpan9 wi simpl13 simpl12 zaddcl 2z
      dvdsmultr1 mp3an1 mpd nncn subsq breqtrrd simpl2 oveq1d nnsqcl 3syl pncan
      simpl11 eqtr3d breqtrd wb adantr cprime 2prm 2nn prmdvdsexp mp3an13 mpbid
      mtand cneg 1nn0 nn0negzi gcdaddm pnncan 3anidm23 subcl mulm1 oveq2d addcl
      negsub eqtrd 2times adantl 3eqtr4d zmulcl sylancr eqbrtrd ppncan 3anidm13
      1z mulid2 cc0 nnne0 ancoms df-ne intnand gcdn0cl syl21anc dvdsgcd syl3anc
      wne sylib mp2and cn0 mulgcd pythagtriplem3 mulid1i syl6eq dvdsprime orel1
      2nn0 2cn sylc ) ADEZBDEZCDEZUAZAFUBGZBFUBGZHGZCFUBGZIZABJGKIZFALMZUCZNZUA
      ZCBOGZCBHGZJGZFIZUCUUHUUGKIZULZUUIUUDUUHUUAYNYSYTUUBUDUUDUUHNZFYOLMZUUAUU
      KFYRYPOGZYOLUUKFUUFUUETGZUUMLUUKFUUFLMZFUUNLMZUUDUUGUUFLMZUUHUUOUUDUUGUUE
      LMZUUQUUDUUEPEZUUFPEZUURUUQNYNYSUUSUUCYLYMUUSYKYMCPEZBPEZUUSYLCUEZBUEZCBU
      FZUGUHUIZUUDUUFDEZUUTUUDYMYLUVGYKYLYMYSUUCUJZYKYLYMYSUUCUKZCBUMZQUUFUERZU
      UEUUFUNQUOUUHUUQUUOUUGFUUFLUPUQURUUKUUTUUSUUOUUPUSZUUKUVAUVBUUTUUKYMUVAYK
      YLYMYSUUCUUHUTZUVCRZUUKYLUVBYKYLYMYSUUCUUHVAZUVDRZCBVBQUUKUVAUVBUUSUVNUVP
      UVEQFPEZUUTUUSUVLVCFUUFUUEVDVEQVFUUKCSEZBSEZUUMUUNIUUKYMUVRUVMCVGZRUUKYLU
      VSUVOBVGZRCBVHQVIUUKYQYPOGZUUMYOUUKYQYRYPOYNYSUUCUUHVJVKUUKYOSEZYPSEZUWBY
      OIUUKYKYODEUWCYKYLYMYSUUCUUHVOAVLYOVGVMUUKYLYPDEUWDUVOBVLYPVGVMYOYPVNQVPV
      QUUKAPEZUULUUAVRZUUDUWEUUHYNYSUWEUUCYKYLUWEYMAUEUIUIVSFVTEZUWEFDEUWFWAWBA
      FFWCWDRWEWFUUDUUGFLMZUUJUUDUUGFBTGZFCTGZJGZFLUUDUUGUWILMZUUGUWJLMZUUGUWKL
      MZUUDUUGUUEUWIJGZUWILUUDUUGUUEUUFKWGZUUETGZHGZJGZUWOUUDUUSUUTUUGUWSIZUVFU
      VKUWPPEUUSUUTUWTKWHWIUWPUUEUUFWJVEQUUDUVRUVSUWSUWOIUUDYMUVRUVHUVTRZUUDYLU
      VSUVIUWARZUVRUVSNZUWRUWIUUEJUXCUUFUUEOGZBBHGZUWRUWIUVRUVSUXDUXEICBBWKWLUX
      CUWRUUFUUEWGZHGZUXDUXCUWQUXFUUFHUXCUUESEZUWQUXFICBWMZUUEWNRWOUXCUUFSEUXHU
      XGUXDICBWPUXIUUFUUEWQQWRUVSUWIUXEIUVRBWSWTXAWOQWRUUDUWOUUELMZUWOUWILMZUUD
      UUSUWIPEZUXJUXKNUVFUUDUVQUVBUXLVCUUDYLUVBUVIUVDRZFBXBXCZUUEUWIUNQUOXDUUDU
      UGUUEUWJJGZUWJLUUDUUGUUEUUFKUUETGZHGZJGZUXOUUDUUSUUTUUGUXRIZUVFUVKKPEUUSU
      UTUXSXGKUUEUUFWJVEQUUDUXQUWJUUEJUUDUVRUVSUXQUWJIUXAUXBUXCUUFUUEHGZCCHGZUX
      QUWJUVRUVSUXTUYAICBCXEXFUXCUXPUUEUUFHUXCUXHUXPUUEIUXIUUEXHRWOUVRUWJUYAIUV
      SCWSVSXAQWOWRUUDUXOUUELMZUXOUWJLMZUUDUUSUWJPEZUYBUYCNUVFUUDUVQUVAUYDVCUUD
      YMUVAUVHUVCRZFCXBXCZUUEUWJUNQUOXDUUDUUGPEZUXLUYDUWLUWMNUWNUSUUDUUGDEZUYGU
      UDUUSUUTUUEXIIZUUFXIIZNUCUYHUVFUVKUUDUYJUYIUUDUUFXIXRZUYJUCYNYSUYKUUCYLYM
      UYKYKYMYLUYKYMYLNUVGUYKUVJUUFXJRXKUHUIUUFXIXLXSXMUUEUUFXNXOZUUGUERUXNUYFU
      UGUWIUWJXPXQXTUUDUWKFBCJGZTGZFUUDUVBUVAUWKUYNIZUXMUYEFYAEUVBUVAUYOYHFBCYB
      VEQUUDUYNFKTGFUUDUYMKFTABCYCWOFYIYDYEWRVQUUDUWGUYHUWHUUJVRWAUYLFUUGYFXCWE
      UUHUUIYGYJ $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  $( Lemma for ~ pythagtrip .  Show that ` C - B ` is positive.  (Contributed
     by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem10 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) -> 0 < ( C - B ) ) $=
    ( cn wcel c2 cexp co clt wbr cc0 cr 3ad2ant1 syl2anc wb resqcl syl 3ad2ant2
    nnre adantr w3a caddc wceq wa wne nnne0 sqgt0 ltaddpos2 mpbid simpr breqtrd
    cmin cle cn0 nnnn0 nn0ge0 3ad2ant3 lt2sq syl22anc mpbird posdif ) ADEZBDEZC
    DEZUAZAFGHZBFGHZUBHZCFGHZUCZUDZBCIJZKCBULHIJZVKVLVGVIIJZVKVGVHVIIVEVGVHIJZV
    JVEKVFIJZVOVEALEZAKUEZVPVBVCVQVDASMZVBVCVRVDAUFMAUGNVEVFLEZVGLEZVPVOOVEVQVT
    VSAPQVEBLEZWAVCVBWBVDBSRZBPQVFVGUHNUITVEVJUJUKVKWBKBUMJZCLEZKCUMJZVLVNOVEWB
    VJWCTZVEWDVJVCVBWDVDVCBUNEWDBUOBUPQRTVEWEVJVDVBWEVCCSUQTZVEWFVJVDVBWFVCVDCU
    NEWFCUOCUPQUQTBCURUSUTVKWBWEVLVMOWGWHBCVANUI $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C - B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem6 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C - B ) ) = ( ( C - B ) gcd A ) ) $=
    ( cn wcel w3a c2 cexp co wceq cgcd c1 wbr cmin cz nnz syl2anc 3ad2ant1 syl
    cc caddc cdivides wn wa csqr cfv cn0 cmul 3ad2ant3 3ad2ant2 pythagtriplem10
    cc0 clt zsubcl 3adant3 elnnz sylanbrc nnnn0 simp3 simp2 3jca pythagtriplem4
    nnaddcl oveq1d 1gcd eqtrd jca oveq1 zcn sqcl 3syl nncn pncan mulcom 3eqtr3d
    subsq coprimeprodsq sylc fveq2d cr cle gcdcl nn0re nn0ge0 sqrsq ) ADEZBDEZC
    DEZFZAGHIZBGHIZUAIZCGHIZJZABKILJGAUBMUCUDZFZCBNIZUEUFWQAKIZGHIZUEUFZWRWPWQW
    SUEWPWQUGEZCBUAIZOEZAUGEZFZWQXBKIZAKIZLJZUDWJWQXBUHIZJWQWSJWPXEXHWPXAXCXDWP
    WQDEZXAWPWQOEZULWQUMMZXJWIWNXKWOWICOEZBOEZXKWHWFXMWGCPUIWGWFXNWHBPUJCBUNQZR
    ZWIWNXLWOABCUKUOWQUPUQWQURSWIWNXCWOWIXBDEZXCWIWHWGXQWFWGWHUSWFWGWHUTCBVCQZX
    BPSRWIWNXDWOWFWGXDWHAURRRVAWPXGLAKIZLWPXFLAKABCVBVDWPAOEZXSLJWIWNXTWOWFWGXT
    WHAPRZRZAVESVFVGWPWLWKNIZWMWKNIZWJXIWNWIYCYDJWOWLWMWKNVHUJWIWNYCWJJZWOWIWJT
    EZWKTEZYEWIXTATEYFYAAVIAVJVKWIBTEZYGWGWFYHWHBVLUJZBVJSWJWKVMQRWIWNYDXIJWOWI
    YDXBWQUHIZXIWICTEZYHYDYJJWHWFYKWGCVLUIYICBVPQWIXBTEZWQTEZYJXIJWIXQYLXRXBVLS
    WIXKYMXOWQVISXBWQVNQVFRVOWQXBAVQVRVSWPWRVTEZULWRWAMZWTWRJWPWRUGEZYNWPXKXTYP
    XPYBWQAWBQZWRWCSWPYPYOYQWRWDSWRWEQVF $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C + B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem7 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C + B ) ) = ( ( C + B ) gcd A ) ) $=
    ( cn wcel w3a c2 cexp co wceq cgcd c1 cmin cz nnz syl syl2anc 3ad2ant1 nncn
    cc caddc cdivides wbr wn csqr cfv cn0 cmul simp3 simp2 zsubcl nnaddcl nnnn0
    wa 3jca pythagtriplem4 oveq1d 1gcd eqtrd jca oveq1 3ad2ant2 pncan subsq zcn
    sqcl mulcom 3eqtr3d coprimeprodsq2 sylc fveq2d cr cc0 cle gcdcl nn0re sqrsq
    nn0ge0 ) ADEZBDEZCDEZFZAGHIZBGHIZUAIZCGHIZJZABKILJGAUBUCUDUNZFZCBUAIZUEUFWJ
    AKIZGHIZUEUFZWKWIWJWLUEWICBMIZNEZWJUGEZAUGEZFZWNWJKIZAKIZLJZUNWCWNWJUHIZJWJ
    WLJWIWRXAWIWOWPWQWBWGWOWHWBCNEZBNEZWOWBWAXCVSVTWAUIZCOPWBVTXDVSVTWAUJZBOPCB
    UKQZRWBWGWPWHWBWJDEZWPWBWAVTXHXEXFCBULQZWJUMPRWBWGWQWHVSVTWQWAAUMRRUOWIWTLA
    KIZLWIWSLAKABCUPUQWIANEZXJLJWBWGXKWHVSVTXKWAAORRZAURPUSUTWIWEWDMIZWFWDMIZWC
    XBWGWBXMXNJWHWEWFWDMVAVBWBWGXMWCJZWHWBWCTEZWDTEZXOWBATEZXPVSVTXRWAASRAVFPWB
    BTEZXQWBVTXSXFBSPZBVFPWCWDVCQRWBWGXNXBJWHWBXNWJWNUHIZXBWBCTEZXSXNYAJWBWAYBX
    ECSPXTCBVDQWBWJTEZWNTEZYAXBJWBXHYCXIWJSPWBWOYDXGWNVEPWJWNVGQUSRVHWNWJAVIVJV
    KWIWKVLEZVMWKVNUCZWMWKJWIWKUGEZYEWIWJNEZXKYGWBWGYHWHWBXHYHXIWJOPRXLWJAVOQZW
    KVPPWIYGYFYIWKVRPWKVQQUS $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C - B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem8 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C - B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr cmin cfv pythagtriplem6 zsubcl syl2anr 3adant1 wne nnne0 df-ne sylib
    csqr intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIM
    KGANUAOPZFCBUBIZULUCVCALIZDABCUDUTVAVDDEZVBUTVCTEZATEZVCQKZAQKZPOZVEURUSVFU
    QUSCTEBTEVFURCRBRCBUEUFUGUQURVGUSARSUQURVJUSUQVIVHUQAQUHVIOAUIAQUJUKUMSVCAU
    NUOSUP $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C + B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem9 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C + B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr csqr cfv pythagtriplem7 zaddcl syl2anr 3adant1 wne nnne0 df-ne sylib
    intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIMKGANU
    AOPZFZCBJIZUBUCVCALIZDABCUDVBVCTEZATEZVCQKZAQKZPOZVDDEUSUTVEVAUQURVEUPURCTE
    BTEVEUQCRBRCBUEUFUGSUSUTVFVAUPUQVFURARSSUSUTVIVAUPUQVIURUPVHVGUPAQUHVHOAUIA
    QUJUKULSSVCAUMUNUO $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  ${
    pythagtriplem11.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` M ` (which will eventually be
       closely related to the ` m ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem11 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> M e. NN ) $=
      ( cn wcel c2 co cdivides wbr wa cz cc0 clt nnz syl wb syl2anc 3ad2ant1 c1
      w3a cexp caddc wceq cgcd wn csqr cmin cdiv pythagtriplem9 simp3r 3ad2ant3
      cfv 3ad2ant2 zaddcl 2z dvdsgcdb mp3an1 simprd mtand pythagtriplem7 breq2d
      biimpar mtbird pythagtriplem8 zsubcl pythagtriplem6 opoe syl22anc nnaddcl
      wne 2ne0 divides2 mp3an12 3syl mpbid nngt0 addgt0 halfpos2 elnnz sylanbrc
      cr nnre syl5eqel ) AFGZBFGZCFGZUBZAHUCIBHUCIUDICHUCIUEZABUFIUAUEZHAJKZUGZ
      LZUBZDCBUDIZUHUNZCBUIIZUHUNZUDIZHUJIZFEWOXAMGZNXAOKZXAFGWOHWTJKZXBWOWQMGZ
      HWQJKZUGWSMGZHWSJKZUGXDWOWQFGZXEABCUKZWQPQWOXFHWPAUFIZJKZWOXLWLWIWJWKWMUL
      ZWOXLLHWPJKZWLWOXNWLLZXLWOWPMGZAMGZXOXLRZWIWJXPWNWICMGZBMGZXPWHWFXSWGCPUM
      ZWGWFXTWHBPUOZCBUPSTWIWJXQWNWFWGXQWHAPTTZHMGZXPXQXRUQHWPAURUSSVDUTVAWOWQX
      KHJABCVBVCVEWOWSFGZXGABCVFZWSPQWOXHHWRAUFIZJKZWOYHWLXMWOYHLHWRJKZWLWOYIWL
      LZYHWOWRMGZXQYJYHRZWIWJYKWNWIXSXTYKYAYBCBVGSTYCYDYKXQYLUQHWRAURUSSVDUTVAW
      OWSYGHJABCVHVCVEWQWSVIVJWOWTFGZWTMGZXDXBRZWOXIYEYMXJYFWQWSVKSZWTPYDHNVLYN
      YOUQVMHWTVNVOVPVQWONWTOKZXCWOWQWCGZWSWCGZNWQOKZNWSOKZYQWOXIYRXJWQWDQWOYEY
      SYFWSWDQWOXIYTXJWQVRQWOYEUUAYFWSVRQWQWSVSVJWOYMWTWCGYQXCRYPWTWDWTVTVPVQXA
      WAWBWE $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` M ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem12 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( M ^ 2 ) = ( ( C + A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cdiv cmul syl syl2anc 3ad2ant1 cc0 2cn
      cc cn w3a cgcd c1 cdivides wn wa csqr cfv cmin nncn addcl syl2anr 3adant1
      oveq1i sqrcl subcl wne 2ne0 sqdiv mp3an23 sqvali oveq2i syl6eq binom2 cle
      cr nnre readdcl clt 3ad2ant3 3ad2ant2 nngt0 addgt0 syl22anc 0re ltle mpan
      sylc resqrth oveq1d resubcl pythagtriplem10 3adant3 oveq12d ppncan 2times
      wi syl3anc eqtr4d oveq1 sqcl pncan subsq 3eqtr3rd fveq2d sqrmul cn0 nnnn0
      nn0ge0 sqrsq 3eqtr3d oveq2d sylancr mp3an1 3eqtr4d 3eqtrd 3adant2 divdiv1
      mulcl add32 adddi pm3.2i divcan3 3eqtr2d eqtrd syl5eq ) AUAFZBUAFZCUAFZUB
      ZAGHIZBGHIZJIZCGHIZKZABUCIUDKGAUELUFUGZUBZDGHICBJIZUHUIZCBUJIZUHUIZJIZGMI
      ZGHIZCAJIZGMIZDYNGHEUOYHYOYMGHIZGGNIZMIZYQYHYMTFZYOYTKYAYFUUAYGYAYJTFZYLT
      FZUUAYAYITFZUUBXSXTUUDXRXTCTFZBTFZUUDXSCUKZBUKZCBULUMUNZYIUPOZYAYKTFZUUCX
      SXTUUKXRXTUUEUUFUUKXSUUGUUHCBUQUMUNZYKUPOZYJYLULPQUUAYOYRGGHIZMIZYTUUAGTF
      ZGRURZYOUUOKSUSYMGUTVAUUNYSYRMGSVBVCVDOYHYTGYPNIZYSMIZUURGMIZGMIZYQYHYRUU
      RYSMYHYRYJGHIZGYJYLNIZNIZJIZYLGHIZJIZYIUVDJIZYKJIZUURYHUUBUUCYRUVGKYAYFUU
      BYGUUJQYAYFUUCYGUUMQYJYLVEPYHUVEUVHUVFYKJYHUVBYIUVDJYHYIVGFZRYIVFLZUVBYIK
      YAYFUVJYGXSXTUVJXRXTCVGFZBVGFZUVJXSCVHZBVHZCBVIUMUNQZYHUVJRYIVJLZUVKUVPYA
      YFUVQYGYAUVLUVMRCVJLZRBVJLZUVQXTXRUVLXSUVNVKXSXRUVMXTUVOVLXTXRUVRXSCVMVKX
      SXRUVSXTBVMVLCBVNVOQRVGFZUVJUVQUVKWHVPRYIVQVRVSZYIVTPWAYHYKVGFZRYKVFLZUVF
      YKKYAYFUWBYGXSXTUWBXRXTUVLUVMUWBXSUVNUVOCBWBUMUNQZYHUWBRYKVJLZUWCUWDYAYFU
      WEYGABCWCWDUVTUWBUWEUWCWHVPRYKVQVRVSZYKVTPWEYHYIYKJIZUVDJIZGCNIZGANIZJIZU
      VIUURYHUWGUWIUVDUWJJYHUWGCCJIZUWIYHUUEUUFUUEUWGUWLKYAYFUUEYGXTXRUUEXSUUGV
      KQZYAYFUUFYGXSXRUUFXTUUHVLQZUWMCBCWFWIYHUUEUWIUWLKUWMCWGOWJYHUVCAGNYHYIYK
      NIZUHUIZYBUHUIZUVCAYHUWOYBUHYHYDYCUJIZYEYCUJIZYBUWOYFYAUWRUWSKYGYDYEYCUJW
      KVLYHYBTFZYCTFZUWRYBKYHATFZUWTYAYFUXBYGXRXSUXBXTAUKZQQZAWLOYHUUFUXAUWNBWL
      OYBYCWMPYHUUEUUFUWSUWOKUWMUWNCBWNPWOWPYHUVJUVKUWBUWCUWPUVCKUVPUWAUWDUWFYI
      YKWQVOYHAVGFZRAVFLZUWQAKYAYFUXEYGXRXSUXEXTAVHQQYAYFUXFYGXRXSUXFXTXRAWRFUX
      FAWSAWTOQQAXAPXBXCWEYHUUDUVDTFZUUKUVIUWHKYAYFUUDYGUUIQYAYFUXGYGYAUUPUVCTF
      ZUXGSYAUUBUUCUXHUUJUUMYJYLXJPGUVCXJXDQYAYFUUKYGUULQYIUVDYKXKWIYHUUEUXBUUR
      UWKKZUWMUXDUUPUUEUXBUXISGCAXLXEPXFXGWAYHUURTFZUVAUUSKZYHUUPYPTFZUXJSYAYFU
      XLYGXRXTUXLXSXTUUEUXBUXLXRUUGUXCCAULUMXHQZGYPXJXDUXJUUPUUQUGZUXNUXKUUPUUQ
      SUSXMZUXOUURGGXIVAOYHUUTYPGMYHUXLUUTYPKZUXMUXLUUPUUQUXPSUSYPGXNVAOWAXOXPX
      Q $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    pythagtriplem13.1 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` N ` (which will eventually be
       closely related to the ` n ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem13 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> N e. NN ) $=
      ( cn wcel c2 co cdivides wbr cz cc0 clt nnz syl wb syl2anc 3ad2ant1 cr c1
      w3a cexp caddc wceq cgcd wn wa csqr cmin cdiv pythagtriplem9 simp3r simp3
      cfv simp2 nnaddcl 2z dvdsgcdb mp3an1 biimpar simprd pythagtriplem7 breq2d
      mtand mtbird pythagtriplem8 3ad2ant3 3ad2ant2 pythagtriplem6 syl22anc zre
      zsubcl omoe simp13 nnre crp ltsubrp nngt0 simp12 ltaddpos mpbid lttrd cle
      nnrp pythagtriplem10 3adant3 wi 0re ltle mpan sylc addgt0 sqrlt nnsub wne
      2ne0 divides2 mp3an12 3syl halfpos2 elnnz sylanbrc syl5eqel ) AFGZBFGZCFG
      ZUBZAHUCIBHUCIUDICHUCIUEZABUFIUAUEZHAJKZUGZUHZUBZDCBUDIZUIUOZCBUJIZUIUOZU
      JIZHUKIZFEXNXTLGZMXTNKZXTFGXNHXSJKZYAXNXPLGZHXPJKZUGXRLGZHXRJKZUGYCXNXPFG
      ZYDABCULZXPOPXNYEHXOAUFIZJKZXNYKXKXHXIXJXLUMZXNYKUHHXOJKZXKXNYMXKUHZYKXNX
      OLGZALGZYNYKQZXHXIYOXMXHXOFGZYOXHXGXFYRXEXFXGUNXEXFXGUPCBUQRZXOOPSXHXIYPX
      MXEXFYPXGAOSSZHLGZYOYPYQURHXOAUSUTRVAVBVEXNXPYJHJABCVCVDVFXNXRFGZYFABCVGZ
      XROPXNYGHXQAUFIZJKZXNUUEXKYLXNUUEUHHXQJKZXKXNUUFXKUHZUUEXNXQLGZYPUUGUUEQZ
      XHXIUUHXMXHCLGZBLGZUUHXGXEUUJXFCOVHXFXEUUKXGBOVICBVMRZSYTUUAUUHYPUUIURHXQ
      AUSUTRVAVBVEXNXRUUDHJABCVJVDVFXPXRVNVKXNXSFGZXSLGZYCYAQZXNXRXPNKZUUMXNXQX
      ONKZUUPXNXQCXOXHXIXQTGZXMXHUUHUURUULXQVLPSZXNXGCTGZXEXFXGXIXMVOCVPPZXHXIX
      OTGZXMXHYRUVBYSXOVPPSZXNUUTBVQGZXQCNKUVAXHXIUVDXMXFXEUVDXGBWEVISCBVRRXNMB
      NKZCXONKZXHXIUVEXMXFXEUVEXGBVSVISZXNBTGZUUTUVEUVFQXNXFUVHXEXFXGXIXMVTBVPP
      ZUVABCWARWBWCXNUURMXQWDKZUVBMXOWDKZUUQUUPQUUSXNUURMXQNKZUVJUUSXHXIUVLXMAB
      CWFWGMTGZUURUVLUVJWHWIMXQWJWKWLUVCXNUVBMXONKZUVKUVCXNUUTUVHMCNKZUVEUVNUVA
      UVIXHXIUVOXMXGXEUVOXFCVSVHSUVGCBWMVKUVMUVBUVNUVKWHWIMXOWJWKWLXQXOWNVKWBXN
      UUBYHUUPUUMQUUCYIXRXPWORWBZXSOUUAHMWPUUNUUOURWQHXSWRWSWTWBXNMXSNKZYBXNUUM
      UVQUVPXSVSPXNUUMXSTGUVQYBQUVPXSVPXSXAWTWBXTXBXCXD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` N ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem14 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( N ^ 2 ) = ( ( C - A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cmin cdiv syl syl2anc 3ad2ant1 cc0 cmul
      cc cn w3a cgcd c1 cdivides wn wa csqr cfv oveq1i nncn addcl syl2anr sqrcl
      subcl 3adant1 wne 2cn 2ne0 sqdiv mp3an23 sqvali oveq2i sqcl pm3.2i simp12
      divdiv1 simp13 binom2sub cr cle nnre readdcl adantl adantr nngt0 syl22anc
      clt addgt0 ltle mpan sylc resqrth resubcl pythagtriplem10 3adant3 oveq12d
      wi oveq1d simp11 subdi mp3an1 ppncan 3anidm13 2times oveq1 3ad2ant2 pncan
      0re eqtr4d subsq 3eqtr3rd fveq2d sqrmul nnnn0 nn0ge0 sqrsq 3eqtr3d oveq2d
      recnd mulcl sylancr addsub syl3anc 3eqtr2rd 3eqtrd 3adant2 divcan3 eqtr3d
      cn0 eqtrd syl5eq ) AUAFZBUAFZCUAFZUBZAGHIZBGHIZJIZCGHIZKZABUCIUDKGAUELUFU
      GZUBZDGHICBJIZUHUIZCBMIZUHUIZMIZGNIZGHIZCAMIZGNIZDYSGHEUJYMYTYRGHIZGGHIZN
      IZUUBYMYRTFZYTUUEKZYFYKUUFYLYDYEUUFYCYDYEUGZYOTFZYQTFZUUFUUHYNTFZUUIYECTF
      ZBTFZUUKYDCUKZBUKZCBULUMYNUNOZUUHYPTFZUUJYEUULUUMUUQYDUUNUUOCBUOUMYPUNOZY
      OYQUOPUPQZUUFGTFZGRUQZUUGURUSYRGUTVAOYMUUEUUCGGSIZNIZUUBUUDUVBUUCNGURVBVC
      YMUUCGNIZGNIZUVCUUBYMUUCTFZUVEUVCKZYMUUFUVFUUSYRVDOUVFUUTUVAUGZUVHUVGUUTU
      VAURUSVEZUVIUUCGGVGVAOYMUVDUUAGNYMUVDGUUASIZGNIZUUAYMUUCUVJGNYMUUCYOGHIZG
      YOYQSIZSIZMIZYQGHIZJIZYNUVNMIZYPJIZUVJYMUUIUUJUUCUVQKYMYDYEUUIYCYDYEYKYLV
      FZYCYDYEYKYLVHZUUPPYMYDYEUUJUVTUWAUURPYOYQVIPYMUVOUVRUVPYPJYMUVLYNUVNMYMY
      NVJFZRYNVKLZUVLYNKYFYKUWBYLYDYEUWBYCYECVJFZBVJFZUWBYDCVLZBVLZCBVMUMZUPQZY
      FYKUWCYLYDYEUWCYCUUHUWBRYNVRLZUWCUWHUUHUWDUWERCVRLZRBVRLZUWJYEUWDYDUWFVNY
      DUWEYEUWGVOYEUWKYDCVPVNYDUWLYEBVPVOCBVSVQRVJFZUWBUWJUWCWHWSRYNVTWAWBUPQZY
      NWCPWIYMYPVJFZRYPVKLZUVPYPKYFYKUWOYLYDYEUWOYCYEUWDUWEUWOYDUWFUWGCBWDUMUPQ
      ZYMUWORYPVRLZUWPUWQYFYKUWRYLABCWEWFUWMUWOUWRUWPWHWSRYPVTWAWBZYPWCPWGYMUVJ
      GCSIZGASIZMIZYNYPJIZUVNMIZUVSYMUULATFZUVJUXBKZYMYEUULUWAUUNOZYMYCUXEYCYDY
      EYKYLWJAUKZOUUTUULUXEUXFURGCAWKWLPYMUXCUWTUVNUXAMYFYKUXCUWTKZYLYDYEUXIYCY
      EUULUUMUXIYDUUNUUOUULUUMUGUXCCCJIZUWTUULUUMUXCUXJKCBCWMWNUULUWTUXJKUUMCWO
      VOWTUMUPQYMUVMAGSYMYNYPSIZUHUIZYGUHUIZUVMAYMUXKYGUHYMYIYHMIZYJYHMIZYGUXKY
      KYFUXNUXOKYLYIYJYHMWPWQYFYKUXNYGKZYLYFYGTFZYHTFZUXPYCYDUXQYEYCUXEUXQUXHAV
      DOQYDYCUXRYEYDUUMUXRUUOBVDOWQYGYHWRPQYMUULUUMUXOUXKKUXGYMYDUUMUVTUUOOCBXA
      PXBXCYMUWBUWCUWOUWPUXLUVMKUWIUWNUWQUWSYNYPXDVQYMAVJFZRAVKLZUXMAKYFYKUXSYL
      YCYDUXSYEAVLQQYFYKUXTYLYCYDUXTYEYCAXTFUXTAXEAXFOQQAXGPXHXIWGYMUUKUUQUVNTF
      ZUXDUVSKYMYNUWIXJYMYPUWQXJYFYKUYAYLYFUUTUVMTFZUYAURYFUUIUUJUYBYDYEUUIYCUU
      PUPYDYEUUJYCUURUPYOYQXKPGUVMXKXLQYNYPUVNXMXNXOXPWIYMUUATFZUVKUUAKZYFYKUYC
      YLYCYEUYCYDYEUULUXEUYCYCUUNUXHCAUOUMXQQUYCUUTUVAUYDURUSUUAGXRVAOYAWIXSYBY
      AYB $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    pythagtriplem15.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    pythagtriplem15.2 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` A ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem15 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> A = ( ( M ^ 2 ) - ( N ^ 2 ) ) ) $=
      ( cn wcel c2 cexp co caddc wceq cmin cdiv cc syl2anc syl 3ad2ant1 cgcd c1
      w3a cdivides wbr wn pythagtriplem12 pythagtriplem14 oveq12d simp3 nnaddcl
      wa simp1 nncn nnz 3ad2ant3 zsubcl zcn cc0 wne 2cn pm3.2i divsubdir mp3an3
      cz 2ne0 cmul pnncan syl3anc 2times eqtr4d oveq1d divcan3 mp3an23 3eqtr2rd
      eqtrd ) AHIZBHIZCHIZUCZAJKLBJKLMLCJKLNZABUALUBNJAUDUEUFULZUCZDJKLZEJKLZOL
      CAMLZJPLZCAOLZJPLZOLZWFWHOLZJPLZAWCWDWGWEWIOABCDFUGABCEGUHUIWCWFQIZWHQIZW
      LWJNZVTWAWMWBVTWFHIZWMVTVSVQWPVQVRVSUJVQVRVSUMCAUKRWFUNSTVTWAWNWBVTWHVEIZ
      WNVTCVEIZAVEIZWQVSVQWRVRCUOUPVQVRWSVSAUOTCAUQRWHURSTWMWNJQIZJUSUTZULWOWTX
      AVAVFVBWFWHJVCVDRWCWLJAVGLZJPLZAWCWKXBJPWCWKAAMLZXBWCCQIZAQIZXFWKXDNVTWAX
      EWBVSVQXEVRCUNUPTVTWAXFWBVQVRXFVSAUNTTZXGCAAVHVIWCXFXBXDNXGAVJSVKVLWCXFXC
      ANZXGXFWTXAXHVAVFAJVMVNSVPVO $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` B ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem16 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> B = ( 2 x. ( M x. N ) ) ) $=
      ( wcel c2 co wceq wbr cmul cdiv cc syl syl2anc 3adant1 3ad2ant1 cc0 caddc
      cn w3a cexp cgcd c1 cdivides wn wa csqr cfv cmin oveq12i nncn addcl sqrcl
      syl2anr subcl wne 2cn 2ne0 pm3.2i divmuldiv mpanr12 mulcl divdiv1 mp3an23
      cr cle nnre readdcl clt adantl adantr nngt0 addgt0 syl22anc 0re ltle mpan
      sylc resqrth resubcl pythagtriplem10 3adant3 oveq12d oveq1d simp12 simp13
      subsq pnncan 3anidm23 2times eqtr4d divcan3 3eqtr3d 3eqtr2d syl5eq oveq2d
      wi eqtrd divcan2 3ad2ant2 eqtr2d ) AUBHZBUBHZCUBHZUCZAIUDJBIUDJUAJCIUDJKZ
      ABUEJUFKIAUGLUHUIZUCZIDEMJZMJIBINJZMJZBXKXLXMIMXKXLCBUAJZUJUKZCBULJZUJUKZ
      UAJZINJZXPXRULJZINJZMJZXMDXTEYBMFGUMXKYCXSYAMJZIIMJNJZYDINJZINJZXMXKXSOHZ
      YAOHZYCYEKZXHXIYHXJXFXGYHXEXFXGUIZXPOHZXROHZYHYKXOOHZYLXGCOHZBOHZYNXFCUNZ
      BUNZCBUOUQXOUPPZYKXQOHZYMXGYOYPYTXFYQYRCBURUQXQUPPZXPXRUOQZRSXHXIYIXJXFXG
      YIXEYKYLYMYIYSUUAXPXRURQZRSYHYIUIIOHZITUSZUIZUUFYJUUDUUEUTVAVBZUUGXSYAIIV
      CVDQXKYDOHZYGYEKZXHXIUUHXJXFXGUUHXEYKYHYIUUHUUBUUCXSYAVEQRSUUHUUFUUFUUIUU
      GUUGYDIIVFVGPXKYFBINXKXPIUDJZXRIUDJZULJZINJXOXQULJZINJZYFBXKUULUUMINXKUUJ
      XOUUKXQULXKXOVHHZTXOVILZUUJXOKXHXIUUOXJXFXGUUOXEXGCVHHZBVHHZUUOXFCVJZBVJZ
      CBVKUQZRSXHXIUUPXJXFXGUUPXEYKUUOTXOVLLZUUPUVAYKUUQUURTCVLLZTBVLLZUVBXGUUQ
      XFUUSVMXFUURXGUUTVNXGUVCXFCVOVMXFUVDXGBVOVNCBVPVQTVHHZUUOUVBUUPWTVRTXOVSV
      TWARSXOWBQXKXQVHHZTXQVILZUUKXQKXHXIUVFXJXFXGUVFXEXGUUQUURUVFXFUUSUUTCBWCU
      QRSZXKUVFTXQVLLZUVGUVHXHXIUVIXJABCWDWEUVEUVFUVIUVGWTVRTXQVSVTWAXQWBQWFWGX
      KUULYDINXKYLYMUULYDKXKXFXGYLXEXFXGXIXJWHZXEXFXGXIXJWIZYSQXKXFXGYMUVJUVKUU
      AQXPXRWJQWGXKUUNIBMJZINJZBXKUUMUVLINXHXIUUMUVLKZXJXFXGUVNXEXGYOYPUVNXFYQY
      RYOYPUIUUMBBUAJZUVLYOYPUUMUVOKCBBWKWLYPUVLUVOKYOBWMVMWNUQRSWGXKYPUVMBKZXK
      XFYPUVJYRPYPUUDUUEUVPUTVABIWOVGPXAWPWGWQWRWSXHXIXNBKZXJXFXEUVQXGXFYPUVQYR
      YPUUDUUEUVQUTVABIXBVGPXCSXD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` C ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem17 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> C = ( ( M ^ 2 ) + ( N ^ 2 ) ) ) $=
      ( cn wcel w3a c2 cexp co caddc wceq wa cdiv cc 3ad2ant1 syl2anc c1 wbr wn
      cgcd cdivides cmin pythagtriplem12 pythagtriplem14 oveq12d 3ad2ant3 addcl
      nncn cc0 wne 2cn 2ne0 pm3.2i divdir mp3an3 cmul ppncan syl3anc 2times syl
      subcl eqtr4d oveq1d divcan3 mp3an23 eqtrd 3eqtr2rd ) AHIZBHIZCHIZJZAKLMBK
      LMNMCKLMOZABUDMUAOKAUEUBUCPZJZDKLMZEKLMZNMCANMZKQMZCAUFMZKQMZNMZWAWCNMZKQ
      MZCVRVSWBVTWDNABCDFUGABCEGUHUIVRWARIZWCRIZWGWEOZVRCRIZARIZWHVOVPWKVQVNVLW
      KVMCULUJSZVOVPWLVQVLVMWLVNAULSSZCAUKTVRWKWLWIWMWNCAVETWHWIKRIZKUMUNZPWJWO
      WPUOUPUQWAWCKURUSTVRWGKCUTMZKQMZCVRWFWQKQVRWFCCNMZWQVRWKWLWKWFWSOWMWNWMCA
      CVAVBVRWKWQWSOWMCVCVDVFVGVRWKWRCOZWMWKWOWPWTUOUPCKVHVIVDVJVK $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    $d A m n $.  $d B m n $.  $d C m n $.
    $( Lemma for ~ pythagtrip .  Wrap the previous ` M ` and ` N ` up in
       quanitifers.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem18 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       E. n e. NN E. m e. NN
       ( A = ( ( m ^ 2 ) - ( n ^ 2 ) ) /\
         B = ( 2 x. ( m x. n ) ) /\
         C = ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) $=
      ( cn wcel w3a c2 cexp caddc wceq csqr cfv cmin cmul oveq1 oveq2d eqeq2d
      co cgcd c1 cdivides wbr wn cdiv wrex eqid pythagtriplem13 pythagtriplem11
      wa pythagtriplem15 pythagtriplem16 pythagtriplem17 oveq2 3anbi123d oveq1d
      cv rcla42ev syl113anc ) AFGBFGCFGHAIJTBIJTKTCIJTLABUATUBLIAUCUDUEUKHCBKTM
      NZCBOTMNZOTIUFTZFGVAVBKTIUFTZFGAVDIJTZVCIJTZOTZLZBIVDVCPTZPTZLZCVEVFKTZLZ
      ADURZIJTZEURZIJTZOTZLZBIVNVPPTZPTZLZCVOVQKTZLZHZDFUGEFUGABCVCVCUHZUIABCVD
      VDUHZUJABCVDVCWGWFULABCVDVCWGWFUMABCVDVCWGWFUNWEVHVKVMHAVOVFOTZLZBIVNVCPT
      ZPTZLZCVOVFKTZLZHEDVCVDFFVPVCLZVSWIWBWLWDWNWOVRWHAWOVQVFVOOVPVCIJQZRSWOWA
      WKBWOVTWJIPVPVCVNPUORSWOWCWMCWOVQVFVOKWPRSUPVNVDLZWIVHWLVKWNVMWQWHVGAWQVO
      VEVFOVNVDIJQZUQSWQWKVJBWQWJVIIPVNVDVCPQRSWQWMVLCWQVOVEVFKWRUQSUPUSUT $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A m n k $.  $d B m n k $.  $d C m n k $.
    $( Lemma for ~ pythagtrip .  Introduce ` k ` and remove the relative
       primality requirement.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem19 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       -. 2 || ( A / ( A gcd B ) ) ) ->
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) $=
      ( cn wcel c2 cexp co wceq wbr cmul wrex cz cc0 3ad2ant1 syl syl3anc caddc
      w3a cgcd cdiv cdivides wn cv cmin wa nnz anim12i wne nnne0 df-ne intnanrd
      sylib adantr gcdn0cl syl2anc 3adant3 c1 gcddvds syl2an simpld wb divides2
      mpbid cr nnre nngt0 divgt0 syl22anc elnnz sylanbrc simprd 3ad2ant2 dvdssq
      clt anbi12d wi nnsqcl dvds2add mpd simpr breqtrd 3ad2ant3 mpbird oveq1 cc
      nncn sqdiv oveq12d divdir syl112anc eqtr4d 3eqtr4d gcddiv syl31anc eqtr3d
      divid simp3 pythagtriplem18 syl312anc divcan2 eqcomd 3jca oveq2 3anbi123d
      eqeq2d syl5ibcom reximdv 2rexbidv rcla4ev rexcom rexbii bitri ) AGHZBGHZC
      GHZUBZAIJKZBIJKZUAKZCIJKZLZIAABUCKZUDKZUEMUFZUBZADUGZEUGZIJKZFUGZIJKZUHKZ
      NKZLZBYJIYKYMNKNKZNKZLZCYJYLYNUAKZNKZLZUBZEGOZFGOZDGOZUUDDGOEGOZFGOZYIYFG
      HZAYFYONKZLZBYFYRNKZLZCYFUUANKZLZUBZEGOZFGOZUUGXTYEUUJYHXQXRUUJXSXQXRUIAP
      HZBPHZUIAQLZBQLZUIUFZUUJXQUUTXRUVAAUJZBUJZUKXQUVDXRXQUVBUVCXQAQULUVBUFAUM
      AQUNUPUOUQABURUSUTZRYIYGYOLZBYFUDKZYRLZCYFUDKZUUALZUBZEGOZFGOZUUSYIYGGHZU
      VIGHZUVKGHZYGIJKZUVIIJKZUAKZUVKIJKZLYGUVIUCKZVALZYHUVOXTYEUVPYHXTYGPHZQYG
      VRMZUVPXTYFAUEMZUWEXTUWGYFBUEMZXQXRUWGUWHUIZXSXQUUTUVAUWIXRUVEUVFABVBVCUT
      ZVDXTYFPHZYFQULZUUTUWGUWEVEXTUUJUWKUVGYFUJSZXTUUJUWLUVGYFUMSZXQXRUUTXSUVE
      RZYFAVFTVGXTAVHHZQAVRMZYFVHHZQYFVRMZUWFXQXRUWPXSAVIRXQXRUWQXSAVJRXTUUJUWR
      UVGYFVISZXTUUJUWSUVGYFVJSZAYFVKVLYGVMVNRXTYEUVQYHXTUVIPHZQUVIVRMZUVQXTUWH
      UXBXTUWGUWHUWJVOXTUWKUWLUVAUWHUXBVEUWMUWNXRXQUVAXSUVFVPZYFBVFTVGXTBVHHZQB
      VRMZUWRUWSUXCXRXQUXEXSBVIVPXRXQUXFXSBVJVPUWTUXABYFVKVLUVIVMVNRXTYEUVRYHXT
      YEUIZUVKPHZQUVKVRMZUVRUXGYFCUEMZUXHUXGUXJYFIJKZYDUEMZUXGUXKYCYDUEXTUXKYCU
      EMZYEXTUXKYAUEMZUXKYBUEMZUIZUXMXTUWIUXPUWJXTUWGUXNUWHUXOXTUWKUUTUWGUXNVEU
      WMUWOYFAVQUSXTUWKUVAUWHUXOVEUWMUXDYFBVQUSVSVGXTUXKPHZYAPHZYBPHZUXPUXMVTXT
      UXKGHZUXQXTUUJUXTUVGYFWASZUXKUJSXTYAGHZUXRXQXRUYBXSAWARZYAUJSXTYBGHZUXSXR
      XQUYDXSBWAVPZYBUJSUXKYAYBWBTWCUQXTYEWDWEXTUXJUXLVEZYEXTUWKCPHZUYFUWMXSXQU
      YGXRCUJWFZYFCVQUSUQWGXTUXJUXHVEZYEXTUWKUWLUYGUYIUWMUWNUYHYFCVFTUQVGXTUXIY
      EXTCVHHZQCVRMZUWRUWSUXIXSXQUYJXRCVIWFXSXQUYKXRCVJWFUWTUXACYFVKVLUQUVKVMVN
      UTYIYCUXKUDKZYDUXKUDKZUWAUWBYEXTUYLUYMLYHYCYDUXKUDWHVPYIUWAYAUXKUDKZYBUXK
      UDKZUAKZUYLXTYEUWAUYPLYHXTUVSUYNUVTUYOUAXTAWIHZYFWIHZUWLUVSUYNLXQXRUYQXSA
      WJRZXTUUJUYRUVGYFWJSZUWNAYFWKTXTBWIHZUYRUWLUVTUYOLXRXQVUAXSBWJVPZUYTUWNBY
      FWKTWLRXTYEUYLUYPLZYHXTYAWIHZYBWIHZUXKWIHZUXKQULZVUCXTUYBVUDUYCYAWJSXTUYD
      VUEUYEYBWJSXTUXTVUFUYAUXKWJSXTUXTVUGUYAUXKUMSYAYBUXKWMWNRWOXTYEUWBUYMLZYH
      XTCWIHZUYRUWLVUHXSXQVUIXRCWJWFZUYTUWNCYFWKTRWPXTYEUWDYHXTYFYFUDKZUWCVAXTU
      UTUVAUUJUWIVUKUWCLUWOUXDUVGUWJABYFWQWRXTUYRUWLVUKVALUYTUWNYFWTUSWSRXTYEYH
      XAYGUVIUVKEFXBXCYIUVNUURFGYIUVMUUQEGYIAYFYGNKZLZBYFUVINKZLZCYFUVKNKZLZUBZ
      UVMUUQXTYEVURYHXTVUMVUOVUQXTVULAXTUYQUYRUWLVULALUYSUYTUWNAYFXDTXEXTVUNBXT
      VUAUYRUWLVUNBLVUBUYTUWNBYFXDTXEXTVUPCXTVUIUYRUWLVUPCLVUJUYTUWNCYFXDTXEXFR
      UVMVUMUULVUOUUNVUQUUPUVHUVJVUMUULVEUVLUVHVULUUKAYGYOYFNXGXIRUVJUVHVUOUUNV
      EUVLUVJVUNUUMBUVIYRYFNXGXIVPUVLUVHVUQUUPVEUVJUVLVUPUUOCUVKUUAYFNXGXIWFXHX
      JXKXKWCUUFUUSDYFGYJYFLZUUDUUQFEGGVUSYQUULYTUUNUUCUUPVUSYPUUKAYJYFYONWHXIV
      USYSUUMBYJYFYRNWHXIVUSUUBUUOCYJYFUUANWHXIXHXLXMUSUUGUUEDGOZFGOUUIUUEDFGGX
      NVUTUUHFGUUDDEGGXNXOXPUP $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A k m n $.  $d B k m n $.  $d C k m n $.
    $( Parameterize the Pythagorean triples.  If ` A ` , ` B ` , and ` C ` are
       naturals, then they obey the Pythagorean triple formula iff they are
       parameterized by three naturals.  This proof follows the Isabelle proof
       at ~ http://afp.sourceforge.net/entries/Fermat3_4.shtml .  (Contributed
       by Scott Fenton, 19-Apr-2014.) $)
    pythagtrip $p |- ( ( A e. NN /\ B e. NN /\ C e. NN ) ->
    ( ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) <->
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) ) $=
      ( cn wcel w3a c2 cexp co wceq cmul wa wrex wo cdiv cdivides 3adant3 caddc
      cpr cv cmin cgcd wn divgcdodd adantr pythagtriplem19 3expia simp12 simp11
      wbr simp13 nnsqcl nncn syl 3ad2ant1 3ad2ant2 addcom syl2anc eqeq1d biimpa
      cc nnz gcdcom oveq2d breq2d notbid biimp3a syl311anc orim12d mpd cvv ovex
      cz preq12bg mpanr12 anbi1d rexbidv 2rexbidv df-3an orbi12i 3ancoma orbi2i
      wb andir 3bitr2i rexbii 2rexbii r19.43 bitri 3bitri syl6bb pythagtriplem2
      mpbird ex wi impbid ) AGHZBGHZCGHZIZAJKLZBJKLZUALZCJKLZMZABUBDUCZEUCZJKLZ
      FUCZJKLZUDLZNLZXIJXJXLNLNLZNLZUBMZCXIXKXMUALNLMZOZDGPZEGPFGPZXCXHYBXCXHOZ
      YBAXOMZBXQMZXSIZDGPZEGPZFGPZBXOMZAXQMZXSIZDGPZEGPZFGPZQZYCJAABUELZRLSUMUF
      ZJBYQRLZSUMZUFZQZYPXCUUBXHWTXAUUBXBABUGTUHYCYRYIUUAYOXCXHYRYIABCDEFUIUJXC
      XHUUAYOXCXHUUAIXAWTXBXEXDUALZXGMZJBBAUELZRLZSUMZUFZYOWTXAXBXHUUAUKWTXAXBX
      HUUAULWTXAXBXHUUAUNXCXHUUDUUAXCXHUUDXCXFUUCXGXCXDVDHZXEVDHZXFUUCMWTXAUUIX
      BWTXDGHUUIAUOXDUPUQURXAWTUUJXBXAXEGHUUJBUOXEUPUQUSXDXEUTVAVBVCTXCXHUUAUUH
      YCYTUUGYCYSUUFJSYCYQUUEBRYCAVPHZBVPHZYQUUEMXCUUKXHWTXAUUKXBAVEURUHXCUULXH
      XAWTUULXBBVEUSUHABVFVAVGVHVIVJBACDEFUIVKUJVLVMXCYBYPWFZXHWTXAUUMXBWTXAOZY
      BYDYEOZYKYJOZQZXSOZDGPZEGPFGPZYPUUNYAUUSFEGGUUNXTUURDGUUNXRUUQXSUUNXOVNHX
      QVNHXRUUQWFXIXNNVOXIXPNVOABXOXQGGVNVNVQVRVSVTWAUUTYFYLQZDGPZEGPFGPYGYMQZE
      GPZFGPZYPUUSUVBFEGGUURUVADGUURUUOXSOZUUPXSOZQYFYKYJXSIZQUVAUUOUUPXSWGYFUV
      FUVHUVGYDYEXSWBYKYJXSWBWCUVHYLYFYKYJXSWDWEWHWIWJUVBUVCFEGGYFYLDGWKWJUVEYH
      YNQZFGPYPUVDUVIFGYGYMEGWKWIYHYNFGWKWLWMWNTUHWPWQWTXAYBXHWRXBABCDEFWOTWS
      $.
      $( [19-Apr-2014] $) $( [19-Apr-2014] $)
  $}

