$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Euler's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c od $.
  $c phi $.

  $( Extend class notation with the order function on the class of integers mod
     N. $)
  cod $a class od $.

  $( Extend class notation with the Euler phi function. $)
  cphi $a class phi $.

  ${
    $d m n x $.
    $( Define the order function on the class of integers mod N. $)
    df-od $a |- od = ( n e. NN |-> ( x e. { x e. ZZ | ( x gcd n ) = 1 } |->
                sup ( { m e. NN | n || ( ( x ^ m ) - 1 ) } , RR , `' < ) ) ) $.

    $( Define the Euler phi function, which counts the number of integers less
       than ` n ` and coprime to it. $)
    df-phi $a |- phi = ( n e. NN |-> ( # `
                         { x e. ( 1 ... n ) | ( x gcd n ) = 1 } ) ) $.
  $}

  ${
    $d n x N $.
    $( Value of the Euler ` phi ` function.  (Contributed by Mario Carneiro,
       23-Feb-2014.) $)
    phival $p |- ( N e. NN -> ( phi ` N ) = ( # `
                         { x e. ( 1 ... N ) | ( x gcd N ) = 1 } ) ) $=
      ( vn cv cgcd co c1 wceq cfz crab chash cfv cn cphi oveq2 eqeq1d rabeqbidv
      fveq2d df-phi fvex fvmpt ) CBADZCDZEFZGHZAGUCIFZJZKLUBBEFZGHZAGBIFZJZKLMN
      UCBHZUGUKKULUEUIAUFUJUCBGIOULUDUHGUCBUBEOPQRACSUKKTUA $.
      $( [23-Feb-2014] $)
  $}

  ${
    $d x N $.
    $( Bounds and closure for the value of the Euler ` phi ` function.
       (Contributed by Mario Carneiro, 23-Feb-2014.) $)
    phicl2 $p |- ( N e. NN -> ( phi ` N ) e. ( 1 ... N ) ) $=
      ( vx cn wcel cfv cgcd co c1 wceq cfz chash cz cle wbr cfn cn0 mp2an 1z wb
      cvv cphi cv crab phival w3a wss fzfi ssrab2 ssfi hashcl ax-mp a1i hashsng
      nn0zi csn cdom snex cuz eluzfz1 nnuz eleq2s nnz mpan gcd1 eqtrd syl oveq1
      gcdcom eqeq1d elrab snssd ssdomg mpsyl snfi hashdom sylibr syl5eqbrr ovex
      sylanbrc rabex mp2 mpbir nnnn0 hashfz1 syl5breq 3jca elfz1 sylancr mpbird
      eqeltrd ) ACDZAUAEBUBZAFGZHIZBHAJGZUCZKEZWOBAUDWKWQWODZWQLDZHWQMNZWQAMNZU
      EZWKWSWTXAWSWKWQWPODZWQPDWOODZWPWOUFZXCHAUGZWNBWOUHZWOWPUIQZWPUJUKUNULWKH
      HUOZKEZWQMHLDZXJHIRHLUMUKWKXIWPUPNZXJWQMNZXITDWKXIWPUFXLHUQWKHWPWKHWODZHA
      FGZHIZHWPDXNAHURECHAUSUTVAWKALDZXPAVBZXQXOAHFGZHXKXQXOXSIRHAVHVCAVDVEVFWN
      XPBHWOWLHIWMXOHWLHAFVGVIVJVSVKXIWPTVLVMXIODXCXMXLSHVNXHXIWPVOQVPVQWKWQWOK
      EZAMWQXTMNZWPWOUPNZWPTDXEYBWNBWOHAJVRVTXGWPWOTVLWAXCXDYAYBSXHXFWPWOVOQWBW
      KAPDXTAIAWCAWDVFWEWFWKXKXQWRXBSRXRWQHAWGWHWIWJ $.
      $( [23-Feb-2014] $)
    $d x N $.
    $( Closure for the value of the Euler ` phi ` function.  (Contributed by
       Mario Carneiro, 28-Feb-2014.) $)
    phicl $p |- ( N e. NN -> ( phi ` N ) e. NN ) $=
      ( cn wcel cphi cfv c1 cfz co phicl2 elfznn syl ) ABCADEZFAGHCLBCAILAJK $.
      $( [28-Feb-2014] $)

    $( Lemma for ~ phibnd . $)
    phibndlem $p |- ( N e. ( ZZ>= ` 2 ) ->
             { x e. ( 1 ... N ) | ( x gcd N ) = 1 } C_ ( 1 ... ( N - 1 ) ) ) $=
      ( c2 cuz cfv wcel cv cgcd co c1 wceq cfz wi wa wne cn wbr syl cr syl2anc
      cmin wral crab wss wn eluz2b2 simplbi wo wb fzm1 nnuz eleq2s biimpa sylan
      clt ord cabs cz eluzelz gcdid cc0 cle nnre cn0 nnnn0 absid eqtrd eluzelre
      nn0ge0 simprbi 1re mp3an1 eqnetrd oveq1 neeq1d syl5ibrcom adantr necon4bd
      ltne syld ralrimiva rabss sylibr ) BCDEFZAGZBHIZJKZWEJBJUAILIZFZMZAJBLIZU
      BWGAWKUCWHUDWDWJAWKWDWEWKFZNZWIWFJWMWIUEZWEBKZWFJOZWDBPFZWLWNWOMWDWQJBUOQ
      ZBUFZUGZWQWLNWIWOWQWLWIWOUHZWLXAUIBJDEPWEJBUJUKULUMUPUNWDWOWPMWLWDWPWOBBH
      IZJOWDXBBJWDXBBUQEZBWDBURFXBXCKCBUSBUTRWDWQXCBKZWTWQBSFZVABVBQZXDBVCWQBVD
      FXFBVEBVIRBVFTRVGWDXEWRBJOZCBVHWDWQWRWSVJJSFXEWRXGVKJBVSVLTVMWOWFXBJWEBBH
      VNVOVPVQVTVRWAWGAWKWHWBWC $.
      $( [23-Feb-2014] $)

    $( A slightly tighter bound on the value of the Euler ` phi ` function.
       (Contributed by Mario Carneiro, 23-Feb-2014.) $)
    phibnd $p |- ( N e. ( ZZ>= ` 2 ) -> ( phi ` N ) <_ ( N - 1 ) ) $=
      ( vx c2 cuz cfv wcel cv cgcd co wceq cfz chash cle wbr cvv wss fzfi mp2an
      c1 cfn crab cmin cphi cdom rabex phibndlem ssdomg mpsyl wb ssrab2 hashdom
      ovex ssfi sylibr cn clt eluz2b2 simplbi phival syl nnm1nn0 hashfz1 eqcomd
      cn0 3syl 3brtr4d ) ACDEFZBGAHISJZBSAKIZUAZLEZSASUBIZKIZLEZAUCEZVLMVGVJVMU
      DNZVKVNMNZVJOFVGVJVMPVPVHBVISAKULUEBAUFVJVMOUGUHVJTFZVMTFVQVPUIVITFVJVIPV
      RSAQVHBVIUJVIVJUMRSVLQVJVMUKRUNVGAUOFZVOVKJVGVSSAUPNAUQURZBAUSUTVGVNVLVGV
      SVLVDFVNVLJVTAVAVLVBVEVCVF $.
      $( [23-Feb-2014] $)
  $}

  $( Value of the Euler ` phi ` function at 1.  (Contributed by Mario Carneiro,
     23-Feb-2014.) $)
  phi1 $p |- ( phi ` 1 ) = 1 $=
    ( c1 cphi cfv csn wcel wceq cfz co cn phicl2 ax-mp cz 1z fzsn eleqtri elsni
    1nn ) ABCZADZERAFRAAGHZSAIERTEQAJKALETSFMANKORAPK $.
    $( [23-Feb-2014] $)

  ${
    $d x N $.
    $( Alternate definition of the Euler ` phi ` function.  (Contributed by
       Mario Carneiro, 23-Feb-2014.) $)
    dfphi2 $p |- ( N e. NN -> ( phi ` N ) = ( # `
                          { x e. ( 0 ... ( N - 1 ) ) | ( x gcd N ) = 1 } ) ) $=
      ( wcel c1 wceq c2 cfv cphi cgcd co cc0 cmin cfz crab chash 0z syl cin wss
      ax-mp cn cuz wo cv elnn1uz2 csn phi1 cz hashsng rabid2 elsni oveq1 syl6eq
      gcd1 mprgbir fveq2i 3eqtr2i fveq2 ax-1cn subidi oveq2d fzsn eqeq1d fveq2d
      oveq2 rabeqbidv 3eqtr4a eluz2b3 simplbi phival caddc ovex fzssp1 eluzelre
      wne cvv cc recnd npcan sylancl syl5sseq sseqin2 sylib oveq1i fzp1ss mp2an
      addid2i eqsstr3i mpbi syl6eqr rabeq 3eqtr4g phibndlem df-ss wi wral wa wn
      inrab2 cabs eluzelz gcd0id cr cle wbr cn0 nnnn0 nn0ge0 3syl absid syl2anc
      eqtrd simprbi eqnetrd neeq1d syl5ibrcom necon2bd wb nnm1nn0 elfzp12 nn0uz
      adantr eleq2s biimpa ord syld eleq2i syl6ib ralrimiva sylibr 3eqtr3d jaoi
      rabss sylbi ) BUACZBDEZBFUBGCZUCBHGZAUDZBIJZDEZAKBDLJZMJZNZOGZEZBUEYPUUFY
      QYPDHGZYSDIJZDEZAKUFZNZOGZYRUUEUUGDUUJOGZUULUGKUHCZUUMDEPKUHUITUUJUUKOUUJ
      UUKEUUIAUUJUUIAUUJUJYSUUJCYSKEZUUIYSKUKUUOUUHKDIJZDYSKDIULUUNUUPDEPKUNTUM
      QUOUPUQBDHURYPUUDUUKOYPUUAUUIAUUCUUJYPUUCKKMJZUUJYPUUBKKMYPUUBDDLJKBDDLUL
      DUSUTUMVAUUNUUQUUJEPKVBTUMYPYTUUHDBDYSIVEVCVFVDVGYQYRUUAADBMJZNZOGZUUEYQY
      OYRUUTEYQYOBDVOZBVHZVIZABVJQYQUUSUUDOYQUUSDUUBMJZRZUUDUVDRZUUSUUDYQUUAAUU
      RUVDRZNZUUAAUUCUVDRZNZUVEUVFYQUVGUVIEUVHUVJEYQUVGUVDUVIYQUVDUURSUVGUVDEYQ
      DUUBDVKJZMJZUVDUURUUBVPCZUVDUVLSBDLVLZDUUBVPVMTYQUVKBDMYQBVQCDVQCUVKBEYQB
      FBVNZVRUSBDVSVTVAWAUVDUURWBWCUVDUUCSUVIUVDEUVDKDVKJZUUBMJZUUCUVPDUUBMDUSW
      GWDZUUNUVMUVQUUCSPUVNKUUBVPWEWFWHUVDUUCWBWIWJUUAAUVGUVIWKQUUAAUURUVDWSUUA
      AUUCUVDWSWLYQUUSUVDSUVEUUSEABWMUUSUVDWNWCYQUUDUVDSZUVFUUDEYQUUAYSUVDCZWOZ
      AUUCWPUVSYQUWAAUUCYQYSUUCCZWQZUUAYSUVQCZUVTUWCUUAUUOWRZUWDYQUUAUWEWOUWBYQ
      UUOYTDYQYTDVOUUOKBIJZDVOYQUWFBDYQUWFBWTGZBYQBUHCUWFUWGEFBXABXBQYQBXCCKBXD
      XEZUWGBEUVOYQYOBXFCUWHUVCBXGBXHXIBXJXKXLYQYOUVAUVBXMXNUUOYTUWFDYSKBIULXOX
      PXQYBUWCUUOUWDYQUWBUUOUWDUCZYQYOUUBXFCUWBUWIXRZUVCBXSUWJUUBKUBGXFYSKUUBXT
      YAYCXIYDYEYFUVQUVDYSUVRYGYHYIUUAAUUCUVDYMYJUUDUVDWNWCYKVDXLYLYN $.
      $( [23-Feb-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z N $.  $d y z ph $.
    hashdvds.1 $e |- ( ph -> N e. NN ) $.
    hashdvds.2 $e |- ( ph -> A e. ZZ ) $.
    hashdvds.3 $e |- ( ph -> B e. ( ZZ>= ` ( A - 1 ) ) ) $.
    hashdvds.4 $e |- ( ph -> C e. ZZ ) $.
    $( The number of numbers in a given residue class in a finite set of
       integers.  (Contributed by Mario Carneiro, 12-Mar-2014.) $)
    hashdvds $p |- ( ph -> ( # ` { x e. ( A ... B ) | N || ( x - C ) } ) =
       ( ( |_ ` ( ( B - C ) / N ) ) - ( |_ ` ( ( ( A - 1 ) - C ) / N ) ) ) ) $=
      ( co wbr cz wcel syl syl2anc syl3anc cle wb adantr vz vy c1 cmin cdiv cfl
      cfv cfz chash cv cdivides crab cen wceq caddc 1z a1i cr cn eluzelz zsubcl
      cuz zre nndivre flcl peano2zm fzen cc ax-1cn recnd addcom sylancr oveq12d
      npcan breqtrd cmul cvv ovex a1i12 wa elfzelz adantl nnz zmulcl zaddcl clt
      elfz elfzle1 zltp1le syl2an mpbird fllt cc0 nnre nngt0 ltdivmul2 ltsubadd
      jca mpbid zlem1lt elfzle2 flge lemuldiv leaddsub mpbir2and dvdsmul2 pncan
      breqtrrd oveq1 breq2d elrab sylanbrc oveq1d wne nncn nnne0 divcan4 eqtr2d
      eleq1 eqeq2d anbi12d syl5ibrcom expimpd wi simprr ad2antrl sylancl ltsub1
      divides2 ltdiv1 lesub1 lediv1 zcn divcan1 sylan2b impbid en2d fzfi mp2an
      cfn entr wss ssrab2 hashen sylibr cn0 eluzle syl3an flwordi subge0 elnn0z
      ssfi hashfz1 eqtr3d ) AUCDEUDKZFUEKZUFUGZCUCUDKZEUDKZFUEKZUFUGZUDKZUHKZUI
      UGZFBUJZEUDKZUKLZBCDUHKZULZUIUGZUVBAUVCUVIUMLZUVDUVJUNZAUVCUVAUCUOKZUUQUH
      KZUMLUVNUVIUMLUVKAUVCUCUVAUOKZUVBUVAUOKZUHKZUVNUMAUCMNZUVBMNZUVAMNZUVCUVQ
      UMLUVRAUPUQAUUQMNZUVTUVSAUUPURNZUWAAUUOURNZFUSNZUWBAUUOMNZUWCADMNZEMNZUWE
      ADUURVBUGNZUWFIUURDUTOZJDEVAPUUOVCOZGUUOFVDPZUUPVEOZAUUTURNZUVTAUUSURNZUW
      DUWMAUUSMNZUWNAUURMNZUWGUWOACMNZUWPHCVFOZJUUREVAPUUSVCOZGUUSFVDPZUUTVEOZU
      UQUVAVAPZUXAUVAUCUVBVGQAUVOUVMUVPUUQUHAUCVHNUVAVHNZUVOUVMUNVIAUVAAUVTUVAU
      RNZUXAUVAVCOZVJZUCUVAVKVLAUUQVHNUXCUVPUUQUNAUUQAUWAUUQURNZUWLUUQVCOZVJUXF
      UUQUVAVNPVMVOAUAUBUVNUVIUAUJZFVPKZEUOKZUBUJZEUDKZFUEKZUVNVQNAUVMUUQUHVRUQ
      AUXIUVNNZUXKVQNUXJEUOVRVSAUXLUVINZUXNVQNUXMFUEVRVSAUXOUXLUXKUNZVTZUXPUXIU
      XNUNZVTZAUXOUXQUXTAUXOVTZUXTUXQUXKUVINZUXIUXKEUDKZFUEKZUNZVTUYAUYBUYEUYAU
      XKUVHNZFUYCUKLZUYBUYAUYFCUXKRLZUXKDRLZUYAUXKMNZUWQUWFUYFUYHUYIVTSUYAUXJMN
      ZUWGUYJUYAUXIMNZFMNZUYKUXOUYLAUXIUVMUUQWAZWBZAUYMUXOAUWDUYMGFWCOZTZUXIFWD
      PZAUWGUXOJTUXJEWEPZAUWQUXOHTZAUWFUXOUWITUXKCDWGQUYAUYHUURUXKWFLZUYAUUSUXJ
      WFLZVUAUYAUUTUXIWFLZVUBUYAVUCUVAUXIWFLZUYAVUDUVMUXIRLZUXOVUEAUXIUVMUUQWHW
      BAUVTUYLVUDVUESUXOUXAUYNUVAUXIWIWJWKAUWMUYLVUCVUDSUXOUWTUYNUUTUXIWLWJWKUY
      AUWNUXIURNZFURNZWMFWFLZVTZVUCVUBSAUWNUXOUWSTUYAUYLVUFUYOUXIVCOZAVUIUXOAVU
      GVUHAUWDVUGGFWNOAUWDVUHGFWOOWRZTZUUSUXIFWPQWSUYAUURURNZEURNZUXJURNZVUBVUA
      SAVUMUXOAUWPVUMUWRUURVCZOZTAVUNUXOAUWGVUNJEVCZOZTZUYAUYKVUOUYRUXJVCOZUURE
      UXJWQQWSUYAUWQUYJUYHVUASUYTUYSCUXKWTPWKUYAUYIUXJUUORLZUYAVVBUXIUUPRLZUYAV
      VCUXIUUQRLZUXOVVDAUXIUVMUUQXAWBAUWBUYLVVCVVDSUXOUWKUYNUUPUXIXBWJWKUYAVUFU
      WCVUIVVBVVCSVUJAUWCUXOUWJTVULUXIUUOFXCQWKUYAVUOVUNDURNZUYIVVBSVVAVUTAVVEU
      XOAUWFVVEUWIDVCZOZTUXJEDXDQWKXEUYAFUXJUYCUKUYAUYLUYMFUXJUKLUYOUYQUXIFXFPU
      YAUXJVHNEVHNZUYCUXJUNUYAUXJVVAVJAVVHUXOAEVUSVJTUXJEXGPZXHUVGUYGBUXKUVHUVE
      UXKUNUVFUYCFUKUVEUXKEUDXIXJXKXLUYAUYDUXJFUEKZUXIUYAUYCUXJFUEVVIXMUYAUXIVH
      NFVHNZFWMXNZVVJUXIUNUYAUXIVUJVJAVVKUXOAUWDVVKGFXOOZTAVVLUXOAUWDVVLGFXPOZT
      UXIFXQQXRWRUXQUXPUYBUXSUYEUXLUXKUVIXSUXQUXNUYDUXIUXQUXMUYCFUEUXLUXKEUDXIX
      MXTYAYBYCAUXPUXSUXRUXPAUXLUVHNZFUXMUKLZVTZUXSUXRYDUVGVVPBUXLUVHUVEUXLUNUV
      FUXMFUKUVEUXLEUDXIXJXKAVVQVTZUXRUXSUXNUVNNZUXLUXNFVPKZEUOKZUNZVTVVRVVSVWB
      VVRVVSUVMUXNRLZUXNUUQRLZVVRUXNMNZUVMMNZUWAVVSVWCVWDVTSVVRVVPVWEAVVOVVPYEV
      VRUYMVVLUXMMNZVVPVWESAUYMVVQUYPTAVVLVVQVVNTZVVRUXLMNZUWGVWGVVOVWIAVVPUXLC
      DWAYFZAUWGVVQJTZUXLEVAPZFUXMYIQWSZAVWFVVQAUVTUVRVWFUXAUPUVAUCWEYGTAUWAVVQ
      UWLTUXNUVMUUQWGQVVRUVAUXNWFLZVWCVVRUUTUXNWFLZVWNVVRUUSUXMWFLZVWOVVRUURUXL
      WFLZVWPVVRCUXLRLZVWQVVOVWRAVVPUXLCDWHYFVVRUWQVWIVWRVWQSAUWQVVQHTVWJCUXLWT
      PWSVVRVUMUXLURNZVUNVWQVWPSAVUMVVQVUQTVVRVWIVWSVWJUXLVCOZAVUNVVQVUSTZUURUX
      LEYHQWSVVRUWNUXMURNZVUIVWPVWOSAUWNVVQUWSTVVRVWGVXBVWLUXMVCOZAVUIVVQVUKTZU
      USUXMFYJQWSVVRUWMVWEVWOVWNSAUWMVVQUWTTVWMUUTUXNWLPWSVVRUVTVWEVWNVWCSAUVTV
      VQUXATVWMUVAUXNWIPWSVVRUXNUUPRLZVWDVVRUXMUUORLZVXEVVRUXLDRLZVXFVVOVXGAVVP
      UXLCDXAYFVVRVWSVVEVUNVXGVXFSVWTAVVEVVQVVGTVXAUXLDEYKQWSVVRVXBUWCVUIVXFVXE
      SVXCAUWCVVQUWJTVXDUXMUUOFYLQWSVVRUWBVWEVXEVWDSAUWBVVQUWKTVWMUUPUXNXBPWSXE
      VVRVWAUXMEUOKZUXLVVRVVTUXMEUOVVRUXMVHNZVVKVVLVVTUXMUNVVRVWGVXIVWLUXMYMOAV
      VKVVQVVMTVWHUXMFYNQXMVVRVWIUWGVXHUXLUNZVWJVWKVWIUXLVHNVVHVXJUWGUXLYMEYMUX
      LEVNWJPXRWRUXSUXOVVSUXQVWBUXIUXNUVNXSUXSUXKVWAUXLUXSUXJVVTEUOUXIUXNFVPXIX
      MXTYAYBYOYCYPYQUVCUVNUVIUUAPUVCYTNUVIYTNZUVLUVKSUCUVBYRUVHYTNUVIUVHUUBVXK
      CDYRUVGBUVHUUCUVHUVIUULYSUVCUVIUUDYSUUEAUVBUUFNZUVDUVBUNAUVSWMUVBRLZVXLUX
      BAVXMUVAUUQRLZAUWMUWBUUTUUPRLZVXNUWTUWKAUUSUUORLZVXOAUURDRLZVXPAUWHVXQIUU
      RDUUGOAUWPUWFUWGVXQVXPSZUWRUWIJUWPVUMUWFVVEUWGVUNVXRVUPVVFVURUURDEYKUUHQW
      SAUWNUWCVUIVXPVXOSUWSUWJVUKUUSUUOFYLQWSUUTUUPUUIQAUXGUXDVXMVXNSUXHUXEUUQU
      VAUUJPWKUVBUUKXLUVBUUMOUUN $.
      $( [12-Mar-2014] $)
  $}

  ${
    $d x y z K $.  $d x y z P $.
    $( Value of the Euler ` phi ` function at a prime power.  (Contributed by
       Mario Carneiro, 24-Feb-2014.) $)
    phiprmpw $p |- ( ( P e. Prime /\ K e. NN ) -> ( phi ` ( P ^ K ) ) =
                     ( ( P ^ ( K - 1 ) ) x. ( P - 1 ) ) ) $=
      ( vx wcel cn wa co cfv c1 wceq crab chash cmin cn0 syl cc cc0 cz cdiv cfl
      cprime cexp cphi cv cgcd cfz cmul prmnn nnnn0 nnexpcl syl2an nnm1nn0 nncn
      phival adantr ax-1cn subdi mp3an3 syl2anc mulid1 oveq2d caddc wbr cun cin
      cdivides c0 inrab wn wral wi elfzelz prmz rpexp syl3an1 3expa an32s simpr
      wb zexpcl gcdcom eqeq1d coprm adantlr 3bitr4d adantl subid1 breq2d notbid
      zcn bitr4d sylan2 biimpd imnan sylib ralrimiva rabeq0 sylibr cfn wss fzfi
      syl5eq ssrab2 ssfi mp2an hashun mp3an12 biimprd con1d orrd unrab syl6reqr
      wo rabid2 fveq2d hashfz1 3syl expm1t sylan 3eqtrd 1z a1i cuz nn0uz subidi
      fveq2i eqtr4i syl6eleq hashdvds oveq1d wne nnne0 expm1 syl3anc flid eqtrd
      0z nnz oveq1i ax-mp eqtr4d eqtri div0 syl6eq oveq12d hashcl nn0cni addcom
      0cn sylancr 3eqtr3rd mulcl subadd mpbird 3eqtrrd ) AUADZBEDZFZABUBGZUCHZC
      UDZUUSUEGZIJZCIUUSUFGZKZLHZABIMGZUBGZAIMGUGGZUURUUSEDZUUTUVFJUUPAEDZBNDZU
      VJUUQAUHZBUIZABUJUKZCUUSUNOUURUVIUVHAUGGZUVHIUGGZMGZUVPUVHMGZUVFUURUVHPDZ
      APDZUVIUVRJZUURUVHEDZUVTUUPUVKUVGNDUWCUUQUVMBULAUVGUJUKZUVHUMOZUUPUWAUUQU
      UPUVKUWAUVMAUMOZUOZUVTUWAIPDUWBUPUVHAIUQURUSUURUVQUVHUVPMUURUVTUVQUVHJUWE
      UVHUTOVAUURUVSUVFJZUVHUVFVBGZUVPJZUURUVEAUVAQMGZVFVCZCUVDKZVDZLHZUVFUWMLH
      ZVBGZUVPUWIUURUVEUWMVEZVGJZUWOUWQJZUURUWRUVCUWLFZCUVDKZVGUVCUWLCUVDVHUURU
      XAVIZCUVDVJUXBVGJUURUXCCUVDUURUVAUVDDZFZUVCUWLVIZVKUXCUXEUVCUXFUXDUURUVAR
      DZUVCUXFVSUVAIUUSVLUURUXGFZUVCAUVAVFVCZVIZUXFUXHUUSUVAUEGZIJZAUVAUEGIJZUV
      CUXJUUPUXGUUQUXLUXMVSZUUPUXGUUQUXNUUPARDZUXGUUQUXNAVMZAUVABVNVOVPVQUXHUVB
      UXKIUXHUXGUUSRDZUVBUXKJUURUXGVRUURUXQUXGUUPUXOUVLUXQUUQUXPUVNABVTUKUOUVAU
      USWAUSWBUUPUXGUXJUXMVSUUQAUVAWCWDWEUXHUWLUXIUXHUWKUVAAVFUXHUVAPDZUWKUVAJU
      XGUXRUURUVAWJWFUVAWGOWHWIWKWLZWMUVCUWLWNWOWPUXACUVDWQWRXBUVEWSDZUWMWSDZUW
      SUWTUVDWSDZUVEUVDWTUXTIUUSXAZUVCCUVDXCUVDUVEXDXEZUYBUWMUVDWTUYAUYCUWLCUVD
      XCUVDUWMXDXEUVEUWMXFXGOUURUWOUVDLHZUUSUVPUURUWNUVDLUURUVDUVCUWLXMZCUVDKZU
      WNUURUYFCUVDVJUVDUYGJUURUYFCUVDUXEUVCUWLUXEUWLUVCUXEUVCUXFUXSXHXIXJWPUYFC
      UVDXNWRUVCUWLCUVDXKXLXOUURUVJUUSNDZUYEUUSJUVOUUSUIZUUSXPXQUUPUWAUUQUUSUVP
      JUWFABXRXSXTUURUWQUVFUVHVBGZUWIUURUWPUVHUVFVBUURUWPUUSQMGZASGZTHZIIMGZQMG
      ZASGZTHZMGUVHQMGZUVHUURCIUUSQAUUPUVKUUQUVMUOZIRDUURYAYBUURUUSNUYNYCHZUURU
      VJUYHUVOUYIONQYCHUYTYDUYNQYCIUPYEZYFYGYHQRDZUURYQYBYIUURUYMUVHUYQQMUURUYM
      UVHTHZUVHUURUYLUVHTUURUYLUUSASGZUVHUURUYKUUSASUURUUSPDZUYKUUSJUURUVJVUEUV
      OUUSUMOUUSWGOYJUURUWAAQYKZBRDZUVHVUDJUWGUURUVKVUFUYSAYLOZUUQVUGUUPBYRWFAB
      YMYNUUAXOUURUVHRDZVUCUVHJUURUWCVUIUWDUVHYROUVHYOOYPUURUYQQTHZQUURUYPQTUUR
      UYPQASGZQUYOQASUYOQQMGQUYNQQMVUAYSQUUIYEUUBYSUURUWAVUFVUKQJUWGVUHAUUCUSXB
      XOVUBVUJQJYQQYOYTUUDUUEUURUVTUYRUVHJUWEUVHWGOXTVAUURUVFPDZUVTUYJUWIJUVFUX
      TUVFNDUYDUVEUUFYTUUGZUWEUVFUVHUUHUUJYPUUKUURUVPPDZUVTVULUWHUWJVSUURUVTUWA
      VUNUWEUWGUVHAUULUSUWEVULUURVUMYBUVPUVHUVFUUMYNUUNUUOYP $.
      $( [24-Feb-2014] $)
  $}

  $( Value of the Euler ` phi ` function at a prime. $)
  phiprm $p |- ( P e. Prime -> ( phi ` P ) = ( P - 1 ) ) $=
    ( cprime wcel c1 cexp co cphi cfv cmin cmul cn 1nn phiprmpw mpan2 cc cz syl
    wceq cc0 ax-1cn prmz zcn exp1 fveq2d subidi oveq2i exp0 syl5eq oveq1d subcl
    sylancl mulid2 eqtrd 3eqtr3d ) ABCZADEFZGHZADDIFZEFZADIFZJFZAGHUTUODKCUQVAR
    LADMNUOUPAGUOAOCZUPARUOAPCVBAUAAUBQZAUCQUDUOVADUTJFZUTUOUSDUTJUOUSASEFZDURS
    AEDTUEUFUOVBVEDRVCAUGQUHUIUOUTOCZVDUTRUOVBDOCVFVCTADUJUKUTULQUMUN $.
    $( [28-Feb-2014] $)


  ${
    $d w y z F $.  $d w x y M $.  $d w x y z ph $.  $d w x y z S $.  $d x T $.
    $d w x y N $.  $d w z U $.  $d w z V $.  $d w z W $.
    crt.1 $e |- S = ( 0 ... ( ( M x. N ) - 1 ) ) $.
    crt.2 $e |- T = ( ( 0 ... ( M - 1 ) ) X. ( 0 ... ( N - 1 ) ) ) $.
    crt.3 $e |- F = ( x e. S |-> <. ( x mod M ) , ( x mod N ) >. ) $.
    crt.4 $e |- ( ph -> ( M e. NN /\ N e. NN /\ ( M gcd N ) = 1 ) ) $.
    $( The Chinese Remainder Theorem: the function that maps ` x ` to its
       remainder classes ` mod M ` and ` mod N ` is 1-1 and onto when ` M ` and
       ` N ` are coprime.  (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    crt $p |- ( ph -> F : S -1-1-onto-> T ) $=
      ( cen wbr wcel wceq cmo co cc0 c1 syl2anc vy vz wf1 cfn wf1o wf cv cfv wi
      weq wral cop cz cmul cmin cfz elfzelz eleq2s wa cxp cn cgcd simp1d adantr
      simpr zmodcl2 simp2d opelxpi syl6eleqr ralrimiva fmpt sylib oveq1 opeq12d
      sylan2 opex fvmpt ad2antrl ad2antll eqeq12d ovex opth syl6bb cdivides nnz
      syl cle clt w3a simprl syl6eleq wb 0z zmulcl elfzm11 sylancr mpbid simprr
      zsubcl simp3d coprmdvds2 syl31anc moddvds syl3anc anbi12d crp zre nnmulcl
      cr nnrp modid syl22anc bitr3d 3imtr4d sylbid ralrimivva sylanbrc syl5eqbr
      dff13 fz01en 3syl cn0 nnnn0 chash nn0mulcl hashfz1 hashxp mp2an oveqan12d
      fzfi syl5eq eqtr4d xpfi hashen syl2an entr xpen xpex ensym eqeltri a1i
      f1finf1o ) ACDEUCZCDLMZDUDNZCDEUEACDEUFZUAUGZEUHZUBUGZEUHZOZUAUBUJZUIZUBC
      UKUACUKUUCABUGZFPQZUUNGPQZULZDNZBCUKUUFAUURBCUUNCNAUUNUMNZUURUUSUUNRFGUNQ
      ZSUOQZUPQZCUUNRUVAUQHURAUUSUSZUUQRFSUOQZUPQZRGSUOQZUPQZUTZDUVCUUOUVENZUUP
      UVGNZUUQUVHNUVCUUSFVANZUVIAUUSVEZAUVKUUSAUVKGVANZFGVBQSOZKVCZVDUUNFVFTUVC
      UUSUVMUVJUVLAUVMUUSAUVKUVMUVNKVGZVDUUNGVFTUUOUUPUVEUVGVHTIVIVOVJBCDUUQEJV
      KVLAUUMUAUBCCAUUGCNZUUICNZUSZUSZUUKUUGFPQZUUIFPQZOZUUGGPQZUUIGPQZOZUSZUUL
      UVTUUKUWAUWDULZUWBUWEULZOUWGUVTUUHUWHUUJUWIUVQUUHUWHOAUVRBUUGUUQUWHCEBUAU
      JUUOUWAUUPUWDUUNUUGFPVMUUNUUGGPVMVNJUWAUWDVPVQVRUVRUUJUWIOAUVQBUUIUUQUWIC
      EBUBUJUUOUWBUUPUWEUUNUUIFPVMUUNUUIGPVMVNJUWBUWEVPVQVSVTUWAUWDUWBUWEUUGFPW
      AUUGGPWAUUIGPWAWBWCUVTFUUGUUIUOQZWDMZGUWJWDMZUSZUUTUWJWDMZUWGUULUVTFUMNZG
      UMNZUWJUMNZUVNUWMUWNUIUVTUVKUWOAUVKUVSUVOVDZFWEZWFZUVTUVMUWPAUVMUVSUVPVDZ
      GWEZWFZUVTUUGUMNZUUIUMNZUWQUVTUXDRUUGWGMZUUGUUTWHMZUVTUUGUVBNZUXDUXFUXGWI
      ZUVTUUGCUVBAUVQUVRWJHWKUVTRUMNZUUTUMNZUXHUXIWLWMUVTUWOUWPUXKUWTUXCFGWNTZU
      UGRUUTWOWPWQZVCZUVTUXERUUIWGMZUUIUUTWHMZUVTUUIUVBNZUXEUXOUXPWIZUVTUUICUVB
      AUVQUVRWRHWKUVTUXJUXKUXQUXRWLWMUXLUUIRUUTWOWPWQZVCZUUGUUIWSTAUVNUVSAUVKUV
      MUVNKWTVDUWJFGXAXBUVTUWCUWKUWFUWLUVTUVKUXDUXEUWCUWKWLUWRUXNUXTUUGUUIFXCXD
      UVTUVMUXDUXEUWFUWLWLUXAUXNUXTUUGUUIGXCXDXEUVTUUGUUTPQZUUIUUTPQZOZUULUWNUV
      TUYAUUGUYBUUIUVTUUGXINZUUTXFNZUXFUXGUYAUUGOUVTUXDUYDUXNUUGXGWFUVTUUTVANZU
      YEUVTUVKUVMUYFUWRUXAFGXHZTZUUTXJWFZUVTUXDUXFUXGUXMVGUVTUXDUXFUXGUXMWTUUGU
      UTXKXLUVTUUIXINZUYEUXOUXPUYBUUIOUVTUXEUYJUXTUUIXGWFUYIUVTUXEUXOUXPUXSVGUV
      TUXEUXOUXPUXSWTUUIUUTXKXLVTUVTUYFUXDUXEUYCUWNWLUYHUXNUXTUUGUUIUUTXCXDXMXN
      XOXPUAUBCDEXSXQACSFUPQZSGUPQZUTZLMZUYMDLMZUUDAUVKUVMUYNUVOUVPUVKUVMUSZCSU
      UTUPQZLMZUYQUYMLMZUYNUYPUYFUXKUYRUYGUUTWEUXKCUVBUYQLHUUTXTXRYAUVKFYBNZGYB
      NZUYSUVMFYCGYCUYTVUAUSZUYQYDUHZUYMYDUHZOZUYSVUBVUCUUTVUDVUBUUTYBNVUCUUTOF
      GYEUUTYFWFVUBVUDUYKYDUHZUYLYDUHZUNQZUUTUYKUDNZUYLUDNZVUDVUHOSFYJZSGYJZUYK
      UYLYGYHUYTVUAVUFFVUGGUNFYFGYFYIYKYLUYQUDNUYMUDNZVUEUYSWLSUUTYJVUIVUJVUMVU
      KVULUYKUYLYMYHUYQUYMYNYHVLYOCUYQUYMYPTTAUVKUVMUYOUVOUVPUYPDUYMLMZUYOUVKUW
      OUWPVUNUVMUWSUXBUWOUWPUSDUVHUYMLIUWOUVEUYKLMUVGUYLLMUVHUYMLMUWPFXTGXTUVEU
      YKUVGUYLSFUPWAZSGUPWAZYQYOXRYODUYMUYKUYLVUOVUPYRYSWFTCUYMDYPTUUEADUVHUDIU
      VEUDNUVGUDNUVHUDNRUVDYJRUVFYJUVEUVGYMYHYTUUACDEUUBXD $.
      $( [24-Feb-2014] $)

    phimul.4 $e |- U = { y e. ( 0 ... ( M - 1 ) ) | ( y gcd M ) = 1 } $.
    phimul.5 $e |- V = { y e. ( 0 ... ( N - 1 ) ) | ( y gcd N ) = 1 } $.
    phimul.6 $e |- W = { y e. S | ( y gcd ( M x. N ) ) = 1 } $.
    $( Lemma for ~ phimul . $)
    phimullem $p |- ( ph ->
                   ( phi ` ( M x. N ) ) = ( ( phi ` M ) x. ( phi ` N ) ) ) $=
      ( wcel wceq vw vz chash cfv cmul co cphi cxp cfn cv cgcd c1 cc0 cmin crab
      cfz wss fzfi ssrab2 ssfi mp2an eqeltri hashxp cen wbr cima wral cmo oveq1
      wa cop eqeq1d elrab2 simplbi opeq12d opex fvmpt syl adantl cz cn syl6eleq
      elfzelz simp1d adantr zmodcl2 syl2anc modgcd cle nnz simpld simprd simp2d
      cdivides gcddvds dvdsmul1 wi wn wne nnne0 simpr necon3ai gcdn0cl syl21anc
      nnmulcl dvdstr syl3anc mp2and dvdslegcd syl31anc simprbi breqtrd nnle1eq1
      3syl mpbid eqtrd sylanbrc dvdsmul2 opelxpi eqeltrd ralrimiva wfun cdm wfn
      wf1o crt f1ofn fnfun eqsstri fndm syl2an cmpt eqtri eqtr3d eqeltrrd sylib
      wb dfphi2 fveq2i syl6eqr syl5sseqr funimass4 mpbird xpss12 sseqtr4i sseli
      ccnv f1ocnvfv2 wf f1ocnv f1of ffvelrn cbvmptv ovex opelxp rpmul funfvima2
      imp syldan ex ssrdv eqssd wf1 f1of1 elexi f1imaen sylancl eqbrtrrd hashen
      xpfi sylibr syl5reqr rabeq ax-mp oveq12d 3eqtr4d ) AKUCUDZFUCUDZJUCUDZUEU
      FZHIUEUFZUGUDZHUGUDZIUGUDZUEUFAUVTFJUHZUCUDZUVQFUISZJUISZUWFUVTTFCUJZHUKU
      FZULTZCUMHULUNUFZUPUFZUOZUIPUWMUISUWNUWMUQUWNUISUMUWLURUWKCUWMUSZUWMUWNUT
      VAVBZJUWIIUKUFZULTZCUMIULUNUFZUPUFZUOZUIQUWTUISUXAUWTUQUXAUISUMUWSURUWRCU
      WTUSZUWTUXAUTVAVBZFJVCVAAUWEKVDVEZUWFUVQTZAGKVFZUWEKVDAUXFUWEAUXFUWEUQZUA
      UJZGUDZUWESZUAKVGZAUXJUAKAUXHKSZVJZUXIUXHHVHUFZUXHIVHUFZVKZUWEUXLUXIUXPTZ
      AUXLUXHDSZUXQUXLUXRUXHUWAUKUFZULTZUWIUWAUKUFZULTZUXTCUXHDKUWIUXHTUYAUXSUL
      UWIUXHUWAUKVIVLRVMZVNZBUXHBUJZHVHUFZUYEIVHUFZVKZUXPDGUYEUXHTUYFUXNUYGUXOU
      YEUXHHVHVIUYEUXHIVHVIVOZNUXNUXOVPVQVRVSUXMUXNFSZUXOJSZUXPUWESUXMUXNUWMSZU
      XNHUKUFZULTZUYJUXMUXHVTSZHWASZUYLUXMUXHUMUWAULUNUFZUPUFZSZUYOUXLUYSAUXLUX
      HDUYRUYDLWBVSUXHUMUYQWCVRZAUYPUXLAUYPIWASZHIUKUFULTZOWDZWEZUXHHWFWGUXMUYM
      UXHHUKUFZULUXMUYOUYPUYMVUETUYTVUDUXHHWHWGUXMVUEULWIVEZVUEULTZUXMVUEUXSULW
      IUXMVUEUXHWNVEZVUEUWAWNVEZVUEUXSWIVEZUXMVUHVUEHWNVEZUXMUYOHVTSZVUHVUKVJUY
      TUXMUYPVULVUDHWJZVRZUXHHWOWGZWKUXMVUKHUWAWNVEZVUIUXMVUHVUKVUOWLUXMVULIVTS
      ZVUPVUNUXMVUAVUQAVUAUXLAUYPVUAVUBOWMZWEZIWJZVRZHIWPWGUXMVUEVTSZVULUWAVTSZ
      VUKVUPVJVUIWQUXMVUEWASZVVBUXMUYOVULUXHUMTZHUMTZVJZWRZVVDUYTVUNUXMUYPHUMWS
      VVHVUDHWTVVGHUMVVEVVFXAXBXNUXHHXCXDZVUEWJVRZVUNUXMUWAWASZVVCUXMUYPVUAVVKV
      UDVUSHIXEZWGZUWAWJVRZVUEHUWAXFXGXHUXMVVBUYOVVCVVEUWAUMTZVJZWRZVUHVUIVJVUJ
      WQVVJUYTVVNUXMVVKUWAUMWSVVQVVMUWAWTVVPUWAUMVVEVVOXAXBXNZVUEUXHUWAXIXJXHUX
      LUXTAUXLUXRUXTUYCXKVSZXLUXMVVDVUFVUGYQVVIVUEXMVRXOXPUWKUYNCUXNUWMFUWIUXNT
      UWJUYMULUWIUXNHUKVIVLPVMXQUXMUXOUWTSZUXOIUKUFZULTZUYKUXMUYOVUAVVTUYTVUSUX
      HIWFWGUXMVWAUXHIUKUFZULUXMUYOVUAVWAVWCTUYTVUSUXHIWHWGUXMVWCULWIVEZVWCULTZ
      UXMVWCUXSULWIUXMVWCUXHWNVEZVWCUWAWNVEZVWCUXSWIVEZUXMVWFVWCIWNVEZUXMUYOVUQ
      VWFVWIVJUYTVVAUXHIWOWGZWKUXMVWIIUWAWNVEZVWGUXMVWFVWIVWJWLUXMVULVUQVWKVUNV
      VAHIXRWGUXMVWCVTSZVUQVVCVWIVWKVJVWGWQUXMVWCWASZVWLUXMUYOVUQVVEIUMTZVJZWRZ
      VWMUYTVVAUXMVUAIUMWSVWPVUSIWTVWOIUMVVEVWNXAXBXNUXHIXCXDZVWCWJVRZVVAVVNVWC
      IUWAXFXGXHUXMVWLUYOVVCVVQVWFVWGVJVWHWQVWRUYTVVNVVRVWCUXHUWAXIXJXHVVSXLUXM
      VWMVWDVWEYQVWQVWCXMVRXOXPUWRVWBCUXOUWTJUWIUXOTUWQVWAULUWIUXOIUKVIVLQVMXQU
      XNUXOFJXSWGXTYAAGYBZKGYCZUQZUXGUXKYQAGDYDZVWSADEGYEZVXBABDEGHILMNOYFZDEGY
      GVRZDGYHVRZADKVWTKUYBCDUOZDRUYBCDUSYIZAVXBVWTDTVXEDGYJVRUUAZUAKUWEGUUBWGU
      UCAUBUWEUXFAUBUJZUWESZVXJUXFSAVXKVJZVXJGUUGZUDZGUDZVXJUXFAVXCVXJESZVXOVXJ
      TVXKVXDUWEEVXJUWEUWMUWTUHZEFUWMUQJUWTUQUWEVXQUQFUWNUWMPUWOYIJUXAUWTQUXBYI
      FUWMJUWTUUDVAMUUEUUFZDEVXJGUUHYKZAVXKVXNKSZVXOUXFSZVXLVXNDSZVXNUWAUKUFZUL
      TZVXTAEDVXMUUIZVXPVYBVXKAVXCEDVXMYEVYEVXDDEGUUJEDVXMUUKXNVXREDVXJVXMUULYK
      ZVXLVXNHUKUFZULTZVXNIUKUFZULTZVYDVXLVXNHVHUFZHUKUFZVYGULVXLVXNVTSZUYPVYLV
      YGTVXLVXNUYRSVYMVXLVXNDUYRVYFLWBVXNUMUYQWCVRZAUYPVXKVUCWEVXNHWHWGVXLVYKUW
      MSZVYLULTZVXLVYKFSZVYOVYPVJVXLVYQVXNIVHUFZJSZVXLVYKVYRVKZUWESVYQVYSVJVXLV
      XJVYTUWEVXLVXOVXJVYTVXSVXLVYBVXOVYTTVYFUAVXNUXPVYTDGUXHVXNTUXNVYKUXOVYRUX
      HVXNHVHVIUXHVXNIVHVIVOGBDUYHYLUADUXPYLNBUADUYHUXPUYIUUMYMVYKVYRVPVQVRYNAV
      XKXAYOVYKVYRFJVXNIVHUUNUUOYPZWKUWKVYPCVYKUWMFUWIVYKTUWJVYLULUWIVYKHUKVIVL
      PVMYPWLYNVXLVYRIUKUFZVYIULVXLVYMVUAWUBVYITVYNAVUAVXKVURWEVXNIWHWGVXLVYRUW
      TSZWUBULTZVXLVYSWUCWUDVJVXLVYQVYSWUAWLUWRWUDCVYRUWTJUWIVYRTUWQWUBULUWIVYR
      IUKVIVLQVMYPWLYNVXLVYMVULVUQVYHVYJVJVYDWQVYNAVULVXKAUYPVULVUCVUMVRWEAVUQV
      XKAVUAVUQVURVUTVRWEVXNHIUUPXGXHUYBVYDCVXNDKUWIVXNTUYAVYCULUWIVXNUWAUKVIVL
      RVMXQAVXTVYAAVWSVXAVXTVYAWQVXFVXIKVXNGUUQWGUURUUSYOUUTUVAUVBADEGUVCZKDUQZ
      UXFKVDVEAVXCWUEVXDDEGUVDVRVXHDEKGKUIDUISWUFKUISZDUYRUILUMUYQURVBVXHDKUTVA
      ZUVEUVFUVGUVHUWEUISZWUGUXEUXDYQUWGUWHWUIUWPUXCFJUVJVAWUHUWEKUVIVAUVKUVLAV
      VKUWBUVQTAUYPVUAVVKVUCVURVVLWGVVKUWBUYBCUYRUOZUCUDUVQCUWAYRKWUJUCKVXGWUJR
      DUYRTVXGWUJTLUYBCDUYRUVMUVNYMYSYTVRAUWCUVRUWDUVSUEAUYPUWCUVRTVUCUYPUWCUWN
      UCUDUVRCHYRFUWNUCPYSYTVRAVUAUWDUVSTVURVUAUWDUXAUCUDUVSCIYRJUXAUCQYSYTVRUV
      OUVP $.
      $( [24-Feb-2014] $)
  $}

  ${
    $d x y M $.  $d x y N $.
    $( The Euler ` phi ` function is a multiplicative function, meaning that it
       distributes over multiplication at relatively prime arguments.
       (Contributed by Mario Carneiro, 24-Feb-2014.) $)
    phimul $p |- ( ( M e. NN /\ N e. NN /\ ( M gcd N ) = 1 ) ->
                   ( phi ` ( M x. N ) ) = ( ( phi ` M ) x. ( phi ` N ) ) ) $=
      ( vx vy cn wcel cgcd co c1 wceq w3a cc0 cmul cmin cfz cxp crab cmo eqid
      cv cop cmpt id phimullem ) AEFBEFABGHIJKZCDLABMHZINHOHZLAINHOHZLBINHOHZPZ
      DTZAGHIJDUHQZCUGCTZARHUMBRHUAUBZABUKBGHIJDUIQZUKUFGHIJDUGQZUGSUJSUNSUEUCU
      LSUOSUPSUD $.
      $( [24-Feb-2014] $)
  $}

  ${
    $d x y z A $.  $d w x y z F $.  $d w x y z G $.  $d w x y z N $.  $d x S $.
    $d w x y z ph $.  $d x y z T $.
    eulerth.1 $e |- ( ph -> ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ) $.
    eulerth.2 $e |- S = { y e. ( 0 ... ( N - 1 ) ) | ( y gcd N ) = 1 } $.
    eulerth.3 $e |- T = ( 1 ... ( phi ` N ) ) $.
    eulerth.4 $e |- ( ph -> F : T -1-1-onto-> S ) $.
    eulerth.5 $e |- G = ( x e. T |-> ( ( A x. ( F ` x ) ) mod N ) ) $.
    $( Lemma for ~ eulerth . $)
    eulerthlem $p |- ( ph -> ( ( A ^ ( phi ` N ) ) mod N ) = ( 1 mod N ) ) $=
      ( co c1 wceq cmul wcel syl vz vw cfv cexp cmo cmin cdivides wbr cseq cgcd
      cle wa cr cn cz simp1d nnre adantr cv wi breq1 anbi2d oveq2 fveq2 oveq12d
      oveq1d eqeq12d oveq2d eqeq1d anbi12d imbi12d simp2d cc0 cfz wf1o cuz nnuz
      syl6eleq syl6eleqr ffvelrn syl2anc oveq1 elrab2 simpld elfzelz zmulcl zre
      wf sylib modabs2 1z ovex fvmpt cc zcn 3eqtr4rd gcdcom simprd eqtrd adantl
      nnz syl3anc cn0 nnnn0 ad2antrl zexpcl wb mpbird sylan adantlr syldan wral
      wss seqcl simp3d rpmul mp2and sylanbrc sseli mp3an2 syl22anc seqp1 eqtr4d
      modmul1 recnd mulcom sylibd mpan2d eleq2i sylan2br w3a wf1 cfn 0z elfzm11
      clt sylancr mpbid moddvds zsubcl cphi phicl leid caddc f1of eluzfz1 seq1i
      crp nnrp exp1 seq1 ax-mp a1i oveq2i syl5eq lep1 peano2re mpand imdistanda
      letr imim1d simprl simprr letrd eluz fzss2 syl6sseqr ssel2 zmodcl2 modgcd
      jca 3eqtrd ralrimiva fmpt crab eqsstri 3syl peano2nn elfz nnge1 mpbir2and
      ssrab2 3expia mul4 expp1 modcl syl221anc 3eqtr4d sylibrd anim12d an12s ex
      a2d syld nnind mpcom mpdan ccnv mulcl mulass f1ocnv cen adantrr eqeqan12d
      ccom adantrl subdi breq2d 3bitr4d coprmdvds modid f1of1 f1fveq ralrimivva
      3bitr3d dff13 cvv eqeltri f1oen fzfid ssfi sylancl f1finf1o f1oco f1oeq23
      sylbid mp2an wfun f1ofun simpr fveq2d eqtr2d seqf1o eqeltrd mulid2 ax-1cn
      fvco3 f1ocnvfv2 subdir 3eqtr2d breqtrd mp3an3 ) ADIUUAUCZUDOZIUEOPIUEOQZI
      VUDPUFOZUGUHZAIVUCRGPUIZUCZVUFROZUGUHZIVUIUJOZPQZVUGAIVUDVUIROZVUCRHPUIZU
      CZUFOZVUJUGAVUNIUEOZVUPIUEOZQZIVUQUGUHZAVUTVUMAVUCVUCUKUHZVUTVUMULZAVUCUM
      SZVVBAVUCUNSZVVDAIUNSZVVEAVVFDUOSZDIUJOZPQZJUPZIUUBTZVUCUQTZVUCUUCTVVEAVV
      BULZVVCAVVEVVBVVKURABUSZVUCUKUHZULZDVVNUDOZVVNVUHUCZROZIUEOZVVNVUOUCZIUEO
      ZQZIVVRUJOZPQZULZUTAPVUCUKUHZULZDPUDOZPVUHUCZROZIUEOZPVUOUCZIUEOZQZIVWJUJ
      OZPQZULZUTAUAUSZVUCUKUHZULZDVWSUDOZVWSVUHUCZROZIUEOZVWSVUOUCZIUEOZQZIVXCU
      JOZPQZULZUTZAVWSPUUDOZVUCUKUHZULZDVXMUDOZVXMVUHUCZROZIUEOZVXMVUOUCZIUEOZQ
      ZIVXQUJOZPQZULZUTZVVMVVCUTBUAVUCVVNPQZVVPVWHVWFVWRVYGVVOVWGAVVNPVUCUKVAVB
      VYGVWCVWOVWEVWQVYGVVTVWLVWBVWNVYGVVSVWKIUEVYGVVQVWIVVRVWJRVVNPDUDVCVVNPVU
      HVDZVEVFVYGVWAVWMIUEVVNPVUOVDVFVGVYGVWDVWPPVYGVVRVWJIUJVYHVHVIVJVKVVNVWSQ
      ZVVPVXAVWFVXKVYIVVOVWTAVVNVWSVUCUKVAVBVYIVWCVXHVWEVXJVYIVVTVXEVWBVXGVYIVV
      SVXDIUEVYIVVQVXBVVRVXCRVVNVWSDUDVCVVNVWSVUHVDZVEVFVYIVWAVXFIUEVVNVWSVUOVD
      VFVGVYIVWDVXIPVYIVVRVXCIUJVYJVHVIVJVKVVNVXMQZVVPVXOVWFVYEVYKVVOVXNAVVNVXM
      VUCUKVAVBVYKVWCVYBVWEVYDVYKVVTVXSVWBVYAVYKVVSVXRIUEVYKVVQVXPVVRVXQRVVNVXM
      DUDVCVVNVXMVUHVDZVEVFVYKVWAVXTIUEVVNVXMVUOVDVFVGVYKVWDVYCPVYKVVRVXQIUJVYL
      VHVIVJVKVVNVUCQZVVPVVMVWFVVCVYMVVOVVBAVVNVUCVUCUKVAVBVYMVWCVUTVWEVUMVYMVV
      TVURVWBVUSVYMVVSVUNIUEVYMVVQVUDVVRVUIRVVNVUCDUDVCVVNVUCVUHVDZVEVFVYMVWAVU
      PIUEVVNVUCVUOVDVFVGVYMVWDVULPVYMVVRVUIIUJVYNVHVIVJVKAVWRVWGAVWOVWQADPGUCZ
      ROZIUEOZIUEOZVYQVWNVWLAVYPUMSZIUUHSZVYRVYQQAVYPUOSZVYSAVVGVYOUOSZWUAAVVFV
      VGVVIJVLZAVYOVMIPUFOZVNOZSZWUBAWUFVYOIUJOZPQZAVYOESZWUFWUHULAFEGWHZPFSZWU
      IAFEGVOZWUJMFEGUUETZAPPVUCVNOZFAVUCPVPUCZSPWUNSAVUCUNWUOVVKVQVRZPVUCUUFTL
      VSZFEPGVTWACUSZIUJOZPQZWUHCVYOWUEEWURVYOQWUSWUGPWURVYOIUJWBVIKWCWIZWDVYOV
      MWUDWETZDVYOWFWAVYPWGTAVVFVYTVVJIUUITZVYPIWJWAAVWMVYQIUEAVYQRHPWKAWUKPHUC
      VYQQWUQBPDVVNGUCZROZIUEOZVYQFHVYGWVEVYPIUEVYGWVDVYODRVVNPGVDVHVFNVYPIUEWL
      WMTUUGVFAVWKVYPIUEAVWIDVWJVYORADWNSZVWIDQAVVGWVGWUCDWOTZDUUJTVWJVYOQZAPUO
      SZWVIWKRGPUUKUULZUUMVEVFWPAVWPIVYOUJOZPVWJVYOIUJWVKUUNAWVLWUGPAIUOSZWUBWV
      LWUGQAVVFWVMVVJIXATZWVBIVYOWQWAAWUFWUHWVAWRWSUUOUVKURVWSUNSZVXLVXOVXKUTVY
      FWVOVXOVXAVXKWVOAVXNVWTWVOAULZVWSVXMUKUHZVXNVWTWVPVWSUMSZWVQWVOWVRAVWSUQZ
      URZVWSUUPZTWVPWVRVXMUMSZVVDWVQVXNULVWTUTWVTWVPWVRWWBWVTVWSUUQZTAVVDWVOVVL
      WTVWSVXMVUCUUTXBUURUUSUVAWVOVXOVXKVYEWVOVXOVXKVYEUTZAWVOVXNWWDAWVOVXNULZU
      LZVXHVYBVXJVYDWWFVXHVXDDVXMGUCZROZROZIUEOZVXFWWHROZIUEOZQZVYBWWFVXDUMSZVX
      FUMSZWWHUOSZVYTVXHWWMUTWWFVXDUOSZWWNWWFVXBUOSZVXCUOSZWWQWWFVVGVWSXCSZWWRA
      VVGWWEWUCURZWVOWWTAVXNVWSXDXEZDVWSXFWAZWWFBCRUOGPVWSWWFVWSUNWUOAWVOVXNUVB
      VQVRZWWFVVNPVWSVNOZSZVVNFSZWVDUOSZWWFWXEFXMWXFWXGWWFWXEWUNFWWFVUCVWSVPUCS
      ZWXEWUNXMWWFWXIVWTWWFVWSVXMVUCWVOWVRAVXNWVSXEZWWFWVRWWBWXJWWCTAVVDWWEVVLU
      RWWFWVRWVQWXJWWATAWVOVXNUVCZUVDWWFVWSUOSZVUCUOSZWXIVWTXGWVOWXLAVXNVWSXAXE
      AWXMWWEAVVEWXMVVKVUCXATURZVWSVUCUVEWAXHVWSPVUCUVFTLUVGWXEFVVNUVHXIZAWXGWX
      HWWEAWXGULZWVDWUESZWXHWXPWXQWVDIUJOZPQZWXPWVDESZWXQWXSULAWUJWXGWXTWUMFEVV
      NGVTXIWUTWXSCWVDWUEEWURWVDQWUSWXRPWURWVDIUJWBVIKWCWIZWDWVDVMWUDWETZXJXKVV
      NUOSWURUOSULZVVNWURROZUOSZWWFVVNWURWFZWTZXNZVXBVXCWFWAVXDWGTWWFVXFUOSZWWO
      WWFBCRUOHPVWSWXDWWFWXFWXGVVNHUCZUOSZWXOAWXGWYKWWEWXPWYJESZWYJWUESWYKAFEHW
      HZWXGWYLAWVFESZBFXLWYMAWYNBFWXPWVFWUESZWVFIUJOZPQZWYNWXPWVEUOSZVVFWYOWXPV
      VGWXHWYRAVVGWXGWUCURZWYBDWVDWFWAZAVVFWXGVVJURZWVEIUVIWAWXPWYPWVEIUJOZIWVE
      UJOZPWXPWYRVVFWYPXUBQWYTXUAWVEIUVJWAWXPWYRWVMXUBXUCQWYTAWVMWXGWVNURZWVEIW
      QWAWXPIDUJOZPQZIWVDUJOZPQZXUCPQZAXUFWXGAXUEVVHPAWVMVVGXUEVVHQWVNWUCIDWQWA
      AVVFVVGVVIJXOWSZURWXPXUGWXRPWXPWVMWXHXUGWXRQXUDWYBIWVDWQWAWXPWXQWXSWYAWRW
      SWXPWVMVVGWXHXUFXUHULXUIUTXUDWYSWYBIDWVDXPXBXQUVLWUTWYQCWVFWUEEWURWVFQWUS
      WYPPWURWVFIUJWBVIKWCXRUVMBFEWVFHNUVNWIZFEVVNHVTXIEWUEWYJEWUTCWUEUVOWUEKWU
      TCWUEUWBUVPZXSWYJVMWUDWEUVQXJXKWYGXNZVXFWGTZWWFVVGWWGUOSZWWPWXAWWFWWGWUES
      ZXUOWWFXUPWWGIUJOZPQZWWFWWGESZXUPXURULWWFWUJVXMFSZXUSAWUJWWEWUMURWWFVXMWU
      NFWWFVXMWUNSZPVXMUKUHZVXNWWFVXMUOSZWXMXVAXVBVXNULXGZWWFVXMUNSZXVCWVOXVEAV
      XNVWSUVRXEZVXMXATWXNXVCWVJWXMXVDWKVXMPVUCUVSXTWAWWFXVEXVBXVFVXMUVTTWXKUWA
      LVSZFEVXMGVTWAWUTXURCWWGWUEEWURWWGQWUSXUQPWURWWGIUJWBVIKWCWIZWDWWGVMWUDWE
      TZDWWGWFWAZAVYTWWEWVCURZWWNWWOULWWPVYTULVXHWWMVXDVXFWWHIYDUWCYAWWFWWJVXSW
      WLVYAWWFWWIVXRIUEWWFWWIVXBDROZVXCWWGROZROZVXRWWFVXBWNSZVXCWNSZWVGWWGWNSZW
      WIXVNQWWFWWRXVOWXCVXBWOTWWFWWSXVPWYHVXCWOTAWVGWWEWVHURZWWFXUOXVQXVIWWGWOT
      VXBVXCDWWGUWDYAWWFVXPXVLVXQXVMRWWFWVGWWTVXPXVLQXVRWXBDVWSUWEWAWWFVWSWUOSZ
      VXQXVMQWXDRGPVWSYBTZVEYCVFWWFWWHIUEOZVXFROZIUEOZWWHVXFROZIUEOZVYAWWLWWFXW
      AUMSZWWHUMSZWYIVYTXWAIUEOXWAQZXWCXWEQWWFXWGVYTXWFWWFWWPXWGXVJWWHWGTZXVKWW
      HIUWFWAZXWIXUMXVKWWFXWGVYTXWHXWIXVKWWHIWJWAXWAWWHVXFIYDUWGWWFVXTXWBIUEWWF
      VXFVXMHUCZROZVXFXWAROZVXTXWBWWFXWKXWAVXFRWWFXUTXWKXWAQXVGBVXMWVFXWAFHVYKW
      VEWWHIUEVYKWVDWWGDRVVNVXMGVDVHVFNWWHIUEWLWMTVHWWFXVSVXTXWLQWXDRHPVWSYBTWW
      FXWAWNSVXFWNSZXWBXWMQWWFXWAXWJYEWWFVXFXUNYEZXWAVXFYFWAUWHVFWWFWWKXWDIUEWW
      FXWNWWHWNSWWKXWDQXWOWWFWWHXWIYEVXFWWHYFWAVFWPVGYGWWFVXJIXVMUJOZPQZVYDWWFV
      XJIWWGUJOZPQZXWQWWFXWRXUQPWWFWVMXUOXWRXUQQAWVMWWEWVNURZXVIIWWGWQWAWWFXUPX
      URXVHWRWSWWFWVMWWSXUOVXJXWSULXWQUTXWTWYHXVIIVXCWWGXPXBYHWWFVYCXWPPWWFVXQX
      VMIUJXVTVHVIUWIUWJUWKUWLUWMUWNUWOUWPUWQZWDAVVFVUNUOSZVUPUOSVUTVVAXGVVJAVU
      DUOSZVUIUOSZXXBAVVGVUCXCSZXXCWUCAVVEXXEVVKVUCXDTDVUCXFWAZABCRUOGPVUCWUPVV
      NWUNSZAWXGWXHFWUNVVNLYIWYBYJZWYCWYEAWYFWTXNZVUDVUIWFWAAVUPVUIUOABCUARWNUB
      GUWRZHUXEZGHPVUCVVNWNSZWURWNSZULZWYDWNSAVVNWURUWSWTXXNWYDWURVVNROQAVVNWUR
      YFWTXXLXXMVWSWNSYKWYDVWSROVVNWURVWSROROQAVVNWURVWSUWTWTWUPAFFXXKVOZWUNWUN
      XXKVOZAEFXXJVOZFEHVOZXXOAWULXXQMFEGUXATZAFEHYLZFEUXBUHZEYMSZXXRAWYMWURHUC
      ZVWSHUCZQZWURVWSQZUTZUAFXLCFXLXXTXUKAXYGCUAFFAWURFSZVWSFSZULZULZXYEIDWURG
      UCZVWSGUCZUFOZROZUGUHZXYFXYKDXYLROZIUEOZDXYMROZIUEOZQZIXYQXYSUFOZUGUHZXYE
      XYPXYKVVFXYQUOSZXYSUOSZYUAYUCXGAVVFXYJVVJURZXYKVVGXYLUOSZYUDAVVGXYJWUCURZ
      XYKYUGVMXYLUKUHZXYLIYPUHZXYKXYLWUESZYUGYUIYUJYKZXYKXYLESZYUKAXYHYUMXYIAWU
      JXYHYUMWUMFEWURGVTXIUXCEWUEXYLXULXSTXYKVMUOSZWVMYUKYULXGYNAWVMXYJWVNURZXY
      LVMIYOYQYRZUPZDXYLWFWAXYKVVGXYMUOSZYUEYUHXYKYURVMXYMUKUHZXYMIYPUHZXYKXYMW
      UESZYURYUSYUTYKZXYKXYMESZYVAAXYIYVCXYHAWUJXYIYVCWUMFEVWSGVTXIUXFEWUEXYMXU
      LXSTXYKYUNWVMYVAYVBXGYNYUOXYMVMIYOYQYRZUPZDXYMWFWAXYQXYSIYSXBXYJXYEYUAXGA
      XYHXYIXYCXYRXYDXYTBWURWVFXYRFHVVNWURQZWVEXYQIUEYVFWVDXYLDRVVNWURGVDVHVFNX
      YQIUEWLWMBVWSWVFXYTFHVYIWVEXYSIUEVYIWVDXYMDRVVNVWSGVDVHVFNXYSIUEWLWMUXDWT
      XYKXYOYUBIUGXYKWVGXYLWNSZXYMWNSZXYOYUBQAWVGXYJWVHURXYKYUGYVGYUQXYLWOTXYKY
      URYVHYVEXYMWOTDXYLXYMUXGXBUXHUXIXYKXYPXUFXYFAXUFXYJXUJURXYKXYPXUFULZIXYNU
      GUHZXYFXYKWVMVVGXYNUOSZYVIYVJUTYUOYUHXYKYUGYURYVKYUQYVEXYLXYMYTWAIDXYNUXJ
      XBXYKXYLIUEOZXYMIUEOZQZXYLXYMQZYVJXYFXYKYVLXYLYVMXYMXYKXYLUMSZVYTYUIYUJYV
      LXYLQXYKYUGYVPYUQXYLWGTAVYTXYJWVCURZXYKYUGYUIYUJYUPVLXYKYUGYUIYUJYUPXOXYL
      IUXKYAXYKXYMUMSZVYTYUSYUTYVMXYMQXYKYURYVRYVEXYMWGTYVQXYKYURYUSYUTYVDVLXYK
      YURYUSYUTYVDXOXYMIUXKYAVGXYKVVFYUGYURYVNYVJXGYUFYUQYVEXYLXYMIYSXBAFEGYLZX
      YJYVOXYFXGAWULYVSMFEGUXLTFEWURVWSGUXMXIUXOYGYHUYFUXNCUAFEHUXPXRAWULXYAMFE
      GFWUNUXQLPVUCVNWLUXRUXSTAWUEYMSEWUEXMXYBAVMWUDUXTXULWUEEUYAUYBFEHUYCXBFEF
      XXJHUYDWAFWUNQZYVTXXOXXPXGLLFWUNFWUNXXKUYEUYGWIAXXGULWXHWVDWNSXXHWVDWOTUB
      USZWUNSAYWAFSZYWAHUCZYWAXXKUCZGUCZQFWUNYWALYIAYWBULZYWEYWCXXJUCZGUCZYWCYW
      FYWDYWGGYWFXXJUYHZWYMYWBYWDYWGQAYWIYWBAXXQYWIXXSEFXXJUYITURAWYMYWBXUKURAY
      WBUYJFEYWAXXJHUYQXBUYKYWFWULYWCESZYWHYWCQAWULYWBMURAWYMYWBYWJXUKFEYWAHVTX
      IFEYWCGUYRWAUYLYJUYMZXXIUYNVUNVUPIYSXBYRAVUQVUNPVUIROZUFOZVUFVUIROZVUJAVU
      PYWLVUNUFAVUPVUIYWLYWKAVUIWNSZYWLVUIQAXXDYWOXXIVUIWOTZVUIUYOTYCVHAVUDWNSZ
      YWOYWNYWMQZAXXCYWQXXFVUDWOTYWPYWQPWNSYWOYWRUYPVUDPVUIUYSXTWAAVUFWNSZYWOYW
      NVUJQAVUFUOSZYWSAXXCWVJYWTXXFWKVUDPYTUYBZVUFWOTYWPVUFVUIYFWAUYTVUAAVUTVUM
      XXAWRAWVMXXDYWTVUKVUMULVUGUTWVNXXIYXAIVUIVUFUXJXBXQAVVFXXCVUEVUGXGZVVJXXF
      VVFXXCWVJYXBWKVUDPIYSVUBWAXH $.
      $( [28-Feb-2014] $)
  $}

  ${
    $d f k x y A $.  $d f k x y N $.
    $( Euler's theorem, a generalization of Fermat's little theorem.  If ` A `
       and ` N ` are coprime, then ` A ^ phi ( N ) == 1 ` , mod ` N ` .
       (Contributed by Mario Carneiro, 28-Feb-2014.) $)
    eulerth $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                    ( ( A ^ ( phi ` N ) ) mod N ) = ( 1 mod N ) ) $=
      ( vk vf vx vy cn wcel cgcd co c1 wceq cfv cfz cv cc0 cmo chash cfn cmul
      cz w3a cphi cmin crab wf1o wex cexp cen wbr cn0 phicl hashfz1 3syl dfphi2
      nnnn0 eqtrd 3ad2ant1 wb fzfi wss ssrab2 ssfi mp2an hashen sylib ovex bren
      rabex wa cmpt simpl oveq1 eqeq1d cbvrabv eqid simpr oveq2d oveq1d cbvmptv
      fveq2 eulerthlem ex exlimdv mpd ) BGHZAUAHZABIJKLZUBZKBUCMZNJZCOZBIJZKLZC
      PBKUDJZNJZUEZDOZUFZDUGZAWJUHJBQJKBQJLZWIWKWQUIUJZWTWIWKRMZWQRMZLZXBWFWGXE
      WHWFXCWJXDWFWJGHWJUKHXCWJLBULWJUPWJUMUNCBUOUQURWKSHWQSHZXEXBUSKWJUTWPSHWQ
      WPVAXFPWOUTWNCWPVBWPWQVCVDWKWQVEVDVFWKWQDWNCWPPWONVGVIVHVFWIWSXADWIWSXAWI
      WSVJEFAWQWKWRCWKAWLWRMZTJZBQJZVKBWIWSVLWNFOZBIJZKLCFWPWLXJLWMXKKWLXJBIVMV
      NVOWKVPWIWSVQCEWKXIAEOZWRMZTJZBQJWLXLLZXHXNBQXOXGXMATWLXLWRWAVRVSVTWBWCWD
      WE $.
      $( [28-Feb-2014] $)
  $}

  $( Fermat's little theorem.  When ` P ` is prime, ` A ^ P == A ` , mod ` P `
     for any ` A ` .  (Contributed by Mario Carneiro, 28-Feb-2014.) $)
  fermltl $p |- ( ( P e. Prime /\ A e. ZZ ) ->
                  ( ( A ^ P ) mod P ) = ( A mod P ) ) $=
    ( wcel cz cexp co cmo wceq cc0 cn sylan w3a c1 cmul cr syl 3ad2ant1 syl2anc
    eqtr4d oveq1d cprime wa cdivides wbr wb prmnn divides3 cmin crp zre 0re a1i
    simp2 cn0 nnm1nn0 zexpcl nnrp simp3 0mod modmul1 syl221anc cc mulcom expm1t
    zcn mul02 3eqtr3d 3expia sylbid wn cgcd coprm prmz gcdcom eqeq1d bitrd cphi
    cfv phicl nnnn0 3syl 1re eulerth syl3an1 phiprm oveq2d mulid2 pm2.61d ) BUA
    CZADCZUBZBAUCUDZABEFZBGFZABGFZHZWKWLWOIHZWPWIBJCZWJWLWQUEBUFZBAUGKWIWJWQWPW
    IWJWQLZAABMUHFZEFZNFZBGFZIXBNFZBGFZWNWOWTAOCZIOCZXBDCZBUICZWOIBGFZHXDXFHWTW
    JXGWIWJWQUMZAUJPXHWTUKULWTWJXAUNCZXIXLWTWRXMWIWJWRWQWSQZBUOPAXAUPRZWTWRXJXN
    BUQZPZWTWOIXKWIWJWQURWTXJXKIHXQBUSPSZAIXBBUTVAWTXCWMBGWTXCXBANFZWMWTAVBCZXB
    VBCZXCXSHWTWJXTXLAVEZPZWTXIYAXOXBVEPZAXBVCRWTXTWRWMXSHZYCXNABVDZRSTWTXFXKWO
    WTXEIBGWTYAXEIHYDXBVFPTXRSVGVHVIWKWLVJZABVKFZMHZWPWKYGBAVKFZMHYIBAVLWKYJYHM
    WIBDCWJYJYHHBVMBAVNKVOVPWIWJYIWPWIWJYILZABVQVRZEFZANFZBGFZMANFZBGFZWNWOYKYM
    OCZMOCZWJXJYMBGFMBGFHZYOYQHYKYMDCZYRYKWJYLUNCZUUAWIWJYIUMZYKWRYLJCUUBWIWJWR
    YIWSQZBVSYLVTWAAYLUPRYMUJPYSYKWBULUUCYKWRXJUUDXPPWIWRWJYIYTWSABWCWDYMMABUTV
    AYKYNWMBGYKYNXSWMYKYMXBANYKYLXAAEWIWJYLXAHYIBWEQWFTYKXTWRYEYKWJXTUUCYBPUUDY
    FRSTYKYPABGYKWJXTYPAHUUCYBAWGWATVGVHVIWH $.
    $( [28-Feb-2014] $)

  $(
  @( A sum formula for the Euler ` phi ` function. @)
  phisum @p |- ( N e. NN -> sum_ k e. { x e. NN | x || N } ( phi ` k ) = N ) @=
    ? @.
    @( [28-Feb-2014] @)
  $)

  ${
    $d m n x N $.  $d n x A $.  $d n K $.
    $( Value of the order function.  This is a function of functions; the inner
       argument selects the base (i.e. mod ` N ` for some ` N ` , often prime)
       and the outer argument selects the integer or equivalence class (if you
       want to think about it that way) from the integers mod ` N ` .  In order
       to ensure the supremum is well-defined, we only define the expression
       when ` A ` and ` N ` are coprime.  (Contributed by Mario Carneiro,
       23-Feb-2014.) $)
    odval $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                  ( ( od ` N ) ` A ) = sup ( { n e. NN |
                    N || ( ( A ^ n ) - 1 ) } , RR , `' < ) ) $=
      ( vx vm cn wcel cz cgcd co c1 wceq cfv cv cexp cmin cdivides wbr crab cr
      cod clt ccnv csup cmpt oveq2 eqeq1d rabbidv oveq1 cbvrabv syl6eqr supeq1d
      wa breq1 mpteq12dv df-od zex rabex mptex fvmpt fveq1d elrab oveq1d breq2d
      eqid wor ltso cnvso mpbi supex sylbir sylan9eq 3impb ) CFGZAHGZACIJZKLZAC
      UAMZMZCABNZOJZKPJZQRZBFSZTUBUCZUDZLVNVOVQUMZVSADVTCIJZKLZBHSZCDNZVTOJZKPJ
      ZQRZBFSZTWEUDZUEZMZWFVNAVRWQECDWKENZIJZKLZDHSZWSWMQRZBFSZTWEUDZUEWQFUAWSC
      LZDXBXEWJWPXFXBWKCIJZKLZDHSWJXFXAXHDHXFWTXGKWSCWKIUFUGUHWIXHBDHVTWKLWHXGK
      VTWKCIUIUGUJUKXFTXDWOWEXFXCWNBFWSCWMQUNUHULUODBEUPDWJWPWIBHUQURUSUTVAWGAW
      JGWRWFLWIVQBAHVTALWHVPKVTACIUIUGVBDAWPWFWJWQWKALZTWOWDWEXIWNWCBFXIWMWBCQX
      IWLWAKPWKAVTOUIVCVDUHULWQVETWDWETUBVFTWEVFVGTUBVHVIVJUTVKVLVM $.
      $( [23-Feb-2014] $)

    $( - Lemma for ~ odcl , showing existence of a recurrent point for the
       exponential.  (Contributed by Mario Carneiro, 28-Feb-2014.) $)
    odcllem $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                    ( ( ( od ` N ) ` A ) e. NN /\
                      N || ( ( A ^ ( ( od ` N ) ` A ) ) - 1 ) ) ) $=
      ( vn cn wcel cz cgcd co wceq cfv cexp cmin cdivides wbr cmo syl2anc oveq2
      c1 oveq1d breq2d w3a cod cv crab wa cr clt ccnv csup odval cuz wss c0 wne
      ssrab2 nnuz sseqtri wrex cphi phicl 3ad2ant1 eulerth wb simp1 simp2 nnnn0
      cn0 syl zexpcl 1z moddvds mp3an3 mpbid rcla4ev infmssuzcl sylancr eqeltrd
      rabn0 sylibr elrab sylib ) BDEZAFEZABGHRIZUAZABUBJJZBACUCZKHZRLHZMNZCDUDZ
      EWFDEBAWFKHZRLHZMNZUEWEWFWKUFUGUHUIZWKACBUJWEWKRUKJZULWKUMUNZWOWKEWKDWPWJ
      CDUOUPUQWEWJCDURZWQWEBUSJZDEZBAWSKHZRLHZMNZWRWBWCWTWDBUTVAZWEXABOHRBOHIZX
      CABVBWEWBXAFEZXEXCVCZWBWCWDVDWEWCWSVGEZXFWBWCWDVEWEWTXHXDWSVFVHAWSVIPWBXF
      RFEXGVJXARBVKVLPVMWJXCCWSDWGWSIZWIXBBMXIWHXARLWGWSAKQSTVNPWJCDVRVSWKRVOVP
      VQWJWNCWFDWGWFIZWIWMBMXJWHWLRLWGWFAKQSTVTWA $.
      $( [28-Feb-2014] $)

    $( The order of a group element is an integer.  (Contributed by Mario
       Carneiro, 28-Feb-2014.) $)
    odcl $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                 ( ( od ` N ) ` A ) e. NN ) $=
      ( cn wcel cz cgcd co c1 wceq w3a cod cfv cexp cdivides wbr odcllem simpld
      cmin ) BCDAEDABFGHIJABKLLZCDBASMGHRGNOABPQ $.
      $( [28-Feb-2014] $)

    $( Any element raised to the power of its order is ` 1 ` .  (Contributed by
       Mario Carneiro, 28-Feb-2014.) $)
    odid $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                      N || ( ( A ^ ( ( od ` N ) ` A ) ) - 1 ) ) $=
      ( cn wcel cz cgcd co c1 wceq w3a cod cfv cexp cdivides wbr odcllem simprd
      cmin ) BCDAEDABFGHIJABKLLZCDBASMGHRGNOABPQ $.
      $( [28-Feb-2014] $)

    $( The only powers of ` A ` that are congruent to ` 1 ` are the multiples
       of the order of ` A ` .  (Contributed by Mario Carneiro,
       28-Feb-2014.) $)
    oddvds $p |- ( ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) /\ K e. NN0 ) ->
                   ( N || ( ( A ^ K ) - 1 ) <-> ( ( od ` N ) ` A ) || K ) ) $=
      ( cn wcel cz co c1 wceq cmo cexp cmin cdivides wbr cc0 cle cr syl syl2anc
      oveq1d vn cgcd w3a cn0 wa cod cfv wn wi clt nn0re adantl odcl adantr nnrp
      crp modlt wb nn0z zmodcl nnre ltnle mpbid cv crab ccnv oveq2 breq2d elrab
      csup cuz ssrab2 nnuz sseqtri infmssuzle mpan sylbir ancoms breq1d syl5ibr
      wss odval mtod imnan sylibr wo elnn0 sylib ord simpl1 nnz dvds0 cc simpl2
      syld exp0 ax-1cn subidi syl6eq breqtrrd syl5ibrcom impbid cdiv cmul nnnn0
      zcn cfl nndivre flcl 0z ax-mp 0re a1i nn0ge0 nngt0 ge0div syl3anc flwordi
      flid syl5eqbrr elnn0z sylanbrc nn0mulcl zexpcl zre expmul 1z odid moddvds
      mpbird modexp syl221anc 1exp 3eqtrd modmul1 caddc expadd modval oveq2d
      1re nn0cn recnd pncan3 eqtr3d mulid2 3syl 3eqtr3d eqeq1d 3bitr3d divides3
      eqtrd simpr 3bitr4d ) CDEZAFEZACUBGHIZUCZBUDEZUEZCABACUFUGUGZJGZKGZHLGZMN
      ZUVAOIZCABKGZHLGMNZUUTBMNZUUSUVDUVEUUSUVDUVADEZUHZUVEUUSUVDUVIUEZUHUVDUVJ
      UIUUSUVKUUTUVAPNZUUSUVAUUTUJNZUVLUHZUUSBQEZUUTUPEZUVMUURUVOUUQBUKULZUUSUU
      TDEZUVPUUQUVRUURACUMUNZUUTUORZBUUTUQSUUSUVAQEZUUTQEZUVMUVNURUUSUVAUDEZUWA
      UUSBFEZUVRUWCUURUWDUUQBUSULZUVSBUUTUTSZUVAUKRUUSUVRUWBUVSUUTVARZUVAUUTVBS
      VCUVKUVLUUSCAUAVDZKGZHLGZMNZUADVEZQUJVFVJZUVAPNZUVIUVDUWNUVIUVDUEUVAUWLEZ
      UWNUWKUVDUAUVADUWHUVAIZUWJUVCCMUWPUWIUVBHLUWHUVAAKVGTVHVIUWLHVKUGZWAUWOUW
      NUWLDUWQUWKUADVLVMVNUVAUWLHVOVPVQVRUUSUUTUWMUVAPUUQUUTUWMIUURAUACWBUNVSVT
      WCUVDUVIWDWEUUSUVIUVEUUSUWCUVIUVEWFUWFUVAWGWHWIWOUUSUVDUVECAOKGZHLGZMNUUS
      COUWSMUUSCFEZCOMNUUSUUNUWTUUNUUOUUPUURWJZCWKRCWLRUUSUWSHHLGOUUSUWRHHLUUSA
      WMEZUWRHIUUSUUOUXBUUNUUOUUPUURWNZAXFRZAWPRTHWQWRWSWTUVEUVCUWSCMUVEUVBUWRH
      LUVAOAKVGTVHXAXBUUSUVFCJGZHCJGZIZUVBCJGZUXFIZUVGUVDUUSUXEUXHUXFUUSAUUTBUU
      TXCGZXGUGZXDGZKGZUVBXDGZCJGZHUVBXDGZCJGZUXEUXHUUSUXMQEZHQEZUVBFEZCUPEZUXM
      CJGZUXFIUXOUXQIUUSUXMFEZUXRUUSUUOUXLUDEZUYCUXCUUSUUTUDEZUXKUDEZUYDUUSUVRU
      YEUVSUUTXERZUUSUXKFEZOUXKPNUYFUUSUXJQEZUYHUUSUVOUVRUYIUVQUVSBUUTXHSZUXJXI
      RZUUSOOXGUGZUXKPOFEUYLOIXJOXSXKUUSOQEZUYIOUXJPNZUYLUXKPNUYMUUSXLXMUYJUUSO
      BPNZUYNUURUYOUUQBXNULUUSUVOUWBOUUTUJNZUYOUYNURUVQUWGUUSUVRUYPUVSUUTXORBUU
      TXPXQVCOUXJXRXQXTUXKYAYBZUUTUXKYCSZAUXLYDSUXMYERUXSUUSYTXMUUSUUOUWCUXTUXC
      UWFAUVAYDSZUUSUUNUYAUXACUORZUUSUYBAUUTKGZUXKKGZCJGZHUXKKGZCJGZUXFUUSUXMVU
      BCJUUSUXBUYEUYFUXMVUBIUXDUYGUYQAUUTUXKYFXQTUUSVUAFEZHFEZUYFUYAVUACJGUXFIZ
      VUCVUEIUUSUUOUYEVUFUXCUYGAUUTYDSZVUGUUSYGXMZUYQUYTUUSVUHCVUAHLGMNZUUQVUKU
      URACYHUNUUSUUNVUFVUGVUHVUKURUXAVUIVUJVUAHCYIXQYJVUAHUXKCYKYLUUSVUDHCJUUSU
      YHVUDHIUYKUXKYMRTYNUXMHUVBCYOYLUUSUXNUVFCJUUSAUXLUVAYPGZKGZUXNUVFUUSUXBUY
      DUWCVUMUXNIUXDUYRUWFAUXLUVAYQXQUUSVULBAKUUSVULUXLBUXLLGZYPGZBUUSUVAVUNUXL
      YPUUSUVOUVPUVAVUNIUVQUVTBUUTYRSYSUUSUXLWMEZBWMEVUOBIUUSUYDVUPUYRUXLUUARUU
      SBUVQUUBUXLBUUCSUUKYSUUDTUUSUXPUVBCJUUSUXTUVBWMEUXPUVBIUYSUVBXFUVBUUEUUFT
      UUGUUHUUSUUNUVFFEZVUGUXGUVGURUXAUUSUUOUURVUQUXCUUQUURUULABYDSVUJUVFHCYIXQ
      UUSUUNUXTVUGUXIUVDURUXAUYSVUJUVBHCYIXQUUIUUSUVRUWDUVHUVEURUVSUWEUUTBUUJSU
      UM $.
      $( [28-Feb-2014] $)

    $( The order of any group element is a divisor of the Euler ` phi `
       function.  (Contributed by Mario Carneiro, 28-Feb-2014.) $)
    odphi $p |- ( ( N e. NN /\ A e. ZZ /\ ( A gcd N ) = 1 ) ->
                  ( ( od ` N ) ` A ) || ( phi ` N ) ) $=
      ( cn wcel cz cgcd co c1 wceq w3a cphi cfv cexp cdivides wbr cod cmo mpbid
      cmin wb eulerth simp1 cn0 simp2 phicl nnnn0 zexpcl syl2anc 1z a1i moddvds
      3syl syl3anc oddvds mpdan ) BCDZAEDZABFGHIZJZBABKLZMGZHSGNOZABPLLUTNOZUSV
      ABQGHBQGIZVBABUAUSUPVAEDZHEDZVDVBTUPUQURUBZUSUQUTUCDZVEUPUQURUDUSUPUTCDVH
      VGBUEUTUFULZAUTUGUHVFUSUIUJVAHBUKUMRUSVHVBVCTVIAUTBUNUOR $.
      $( [28-Feb-2014] $)
  $}

