$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                 Algorithms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d F k m $.  $d N k m $.  $d k m ph $.
    nn0seqcvgd.1 $e |- ( ph -> F : NN0 --> NN0 ) $.
    nn0seqcvgd.2 $e |- ( ph -> N = ( F ` 0 ) ) $.
    nn0seqcvgd.3 $e |- ( ( ph /\ k e. NN0 ) ->
      ( ( F ` ( k + 1 ) ) =/= 0 -> ( F ` ( k + 1 ) ) < ( F ` k ) ) ) $.
    $( A strictly-decreasing nonnegative integer sequence with initial term
       ` N ` reaches zero by the ` N ` th term.  Deduction version.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    nn0seqcvgd $p |- ( ph -> ( F ` N ) = 0 ) $=
      ( cfv cc0 wceq cle wbr cmin co cn0 wcel wi cr syl wa 0nn0 ffvelrn sylancl
      vm wf eqeltrd nn0re cv c1 caddc fveq2 oveq2 breq12d imbi2d eqbrtrrd recnd
      leid cc subid1 breqtrrd a1i clt wb posdif syl2anr adantr adantl peano2nn0
      breq1 cz syl2an zsubcl zltlem1 syl2anc nn0cn ax-1cn subsub4 mp3an3 breq2d
      bitrd 3bitr2d biimpa an32s a1d wne sylan zre ltletr syl3anc sylibd syland
      nn0z expdimp pm2.61dane anasss expcom a2d 3adant1 fnn0ind pm2.43i breqtrd
      subid nn0ge0 0re letri3 mpan2 3syl mpbir2and ) ADCHZIJZXIIKLZIXIKLZAXIDDM
      NZIKAXIXMKLZADOPZXODDKLZAXNQZADICHZOFAOOCUEZIOPXROPEUAOOICUBUCUFZXTADRPZX
      PAXOYAXTDUGSZDUQSZAUDUHZCHZDYDMNZKLZQAXRDIMNZKLZQZABUHZCHZDYKMNZKLZQZAYKU
      IUJNZCHZDYPMNZKLZQZXQUDBDDYDIJZYGYIAUUAYEXRYFYHKYDICUKYDIDMULUMUNYDYKJZYG
      YNAUUBYEYLYFYMKYDYKCUKYDYKDMULUMUNYDYPJZYGYSAUUCYEYQYFYRKYDYPCUKYDYPDMULU
      MUNYDDJZYGXNAUUDYEXIYFXMKYDDCUKYDDDMULUMUNYJXOAXRDYHKADXRDKFYCUOADURPZYHD
      JADYBUPZDUSSUTVAYKOPZYKDVBLZYOYTQXOUUGUUHTZAYNYSAUUIYNYSQZAUUGUUHUUJAUUGT
      ZUUHTZUUJYQIUULYQIJZTYSYNUUKUUMUUHYSUUKUUMTZUUHYSUUNUUHIYMVBLZYQYMVBLZYSU
      UKUUHUUOVCZUUMUUGYKRPYAUUQAYKUGYBYKDVDVEVFUUMUUPUUOVCUUKYQIYMVBVIVGUUKUUP
      YSVCUUMUUKUUPYQYMUIMNZKLZYSUUKYQVJPZYMVJPZUUPUUSVCUUKYQOPZUUTAXSYPOPUVBUU
      GEYKVHOOYPCUBVKZYQWLSADVJPZYKVJPUVAUUGAXOUVDXTDWLSYKWLDYKVLVKZYQYMVMVNUUK
      UURYRYQKAUUEYKURPZUURYRJZUUGUUFYKVOUUEUVFUIURPUVGVPDYKUIVQVRVKVSVTZVFWAWB
      WCWDUULYQIWEZYNYSUUKUVIYNTYSQUUHUUKUVIYQYLVBLZYNYSGUUKUVJYNTZUUPYSUUKYQRP
      ZYLRPZYMRPZUVKUUPQUUKUVBUVLUVCYQUGSUUKYLOPZUVMAXSUUGUVOEOOYKCUBWFYLUGSUUK
      UVAUVNUVEYMWGSYQYLYMWHWIUVHWJWKVFWMWNWOWPWQWRWSWIWTAUUEXMIJUUFDXBSXAAXIOP
      ZXLAXSXOUVPEXTOODCUBVNZXIXCSAUVPXIRPZXJXKXLTVCZUVQXIUGUVRIRPUVSXDXIIXEXFX
      GXH $.
      $( [31-Mar-2011] $)
  $}

  ${
    algrflem.1 $e |- B e. _V $.
    $( Lemma for ~ algrf and related theorems.  (Contributed by Mario Carneiro,
       28-May-2014.)  (Revised 27-Dec-2014.) $)
    algrflem $p |- ( B ( F o. 1st ) C ) = ( F ` B ) $=
      ( c1st ccom co cop cfv df-ov cvv wcel wceq wfo fo1st fof ax-mp opex fvco3
      wf mp2an op1st fveq2i 3eqtri ) ABCEFZGABHZUEIZUFEIZCIZACIABUEJKKETZUFKLUG
      UIMKKENUJOKKEPQABRKKUFCESUAUHACABDUBUCUD $.
      $( [27-Dec-2014] $) $( [28-May-2014] $)
  $}

  ${
    $d x y A $.  $d x y F $.  $d x y M $.  $d x y ph $.  $d x y S $.
    $d x y V $.  $d x y Z $.
    algrf.1 $e |- Z = ( ZZ>= ` M ) $.
    algrf.2 $e |- R = seq M ( ( F o. 1st ) , ( Z X. { A } ) ) $.
    $( A sequence whose iteration function ignores the second argument is only
       affected by the first point of the initial value function.  (Contributed
       by Mario Carneiro, 11-Feb-2015.) $)
    seq1st $p |- ( ( M e. ZZ /\ A e. V ) ->
      R = seq M ( ( F o. 1st ) , { <. M , A >. } ) ) $=
      ( vx vy cz wcel cfv adantr wceq wi co fveq2 eqeq12d imbi2d c1st ccom cseq
      wa csn cxp cop cuz wfn seqfn cv c1 caddc uzid syl6eleqr fvconst2g syl2anr
      id fvsng eqtr4d seq1 3eqtr4d ex seqp1 fvex algrflem syl6eq adantl syl5ibr
      wb expcom a2d uzind4 impcom adantll eqfnfvd syl5eq ) DKLZAELZUDZBCUAUBZFA
      UEUFZDUCZWADAUGUEZDUCZHVTIDUHMZWCWEVRWCWFUIVSWAWBDUJNVRWEWFUIVSWAWDDUJNVS
      IUKZWFLZWGWCMZWGWEMZOZVRWHVSWKVSJUKZWCMZWLWEMZOZPVSDWCMZDWEMZOZPVSWKPZVSW
      GULUMQZWCMZWTWEMZOZPWSJIDWGWLDOZWOWRVSXDWMWPWNWQWLDWCRWLDWERSTWLWGOZWOWKV
      SXEWMWIWNWJWLWGWCRWLWGWERSTZWLWTOZWOXCVSXGWMXAWNXBWLWTWCRWLWTWERSTXFVRVSW
      RVTDWBMZDWDMZWPWQVTXHAXIVSVSDFLXHAOVRVSURVRDWFFDUNGUOFADEUPUQDAKEUSUTVRWP
      XHOVSWAWBDVANVRWQXIOVSWAWDDVANVBVCWHVSWKXCVSWHWKXCPWKXCVSWHUDWICMZWJCMZOZ
      WIWJCRWHXCXLVJVSWHXAXJXBXKWHXAWIWTWBMZWAQXJWAWBDWGVDWIXMCWGWCVEVFVGWHXBWJ
      WTWDMZWAQXKWAWDDWGVDWJXNCWGWEVEVFVGSVHVIVKVLVMVNVOVPVQ $.
      $( [11-Feb-2015] $)

    algrf.3 $e |- ( ph -> M e. ZZ ) $.
    algrf.4 $e |- ( ph -> A e. S ) $.
    $( The value of the algorithm iterator ` R ` at ` 0 ` is the initial state
       ` A ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algr0 $p |- ( ph -> ( R ` M ) = A ) $=
      ( cfv c1st ccom csn cxp cseq wcel wceq syl fveq1i seq1 cuz uzid syl6eleqr
      cz fvconst2g syl2anc eqtrd syl5eq ) AFCLFEMNZGBOPZFQZLZBFCUMIUAAUNFULLZBA
      FUFRZUNUOSJUKULFUBTABDRFGRUOBSKAFFUCLZGAUPFUQRJFUDTHUEGBFDUGUHUIUJ $.
      $( [28-May-2014] $) $( [31-Mar-2011] $)

    algrf.5 $e |- ( ph -> F : S --> S ) $.
    $( An algorithm is step a function ` F : S --> S ` on a state space
       ` S ` .  An algorithm acts on an initial state ` A e. S ` by iteratively
       applying ` F ` to give ` A ` , ` ( F `` A ) ` , ` ( F `` ( F `` A ) ) `
       and so on.  An algorithm is said to halt if a fixed point of ` F ` is
       reached after a finite number of iterations.

       The algorithm iterator ` R : NN0 --> S ` "runs" the algorithm ` F ` so
       that ` ( R `` k ) ` is the state after ` k ` iterations of ` F ` on the
       initial state ` A ` .

       Domain and codomain of the algorithm iterator ` R ` .  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algrf $p |- ( ph -> R : Z --> S ) $=
      ( vx vy c1st wf cv wcel wa cfv ccom csn cseq wceq fvconst2g sylan eqeltrd
      cxp adantr vex algrflem simpl ffvelrn syl2an syl5eqel seqf feq1i sylibr
      co ) AGDEOUAZGBUBUHZFUCZPGDCPAMNUTDVAFGHJAMQZGRZSVCVATZBDABDRZVDVEBUDKGBV
      CDUEUFAVFVDKUIUGAVCDRZNQZDRZSZSVCVHUTUSVCETZDVCVHEMUJUKADDEPVGVKDRVJLVGVI
      ULDDVCEUMUNUOUPGDCVBIUQUR $.
      $( [28-May-2014] $) $( [31-Mar-2011] $)

    $( The value of the algorithm iterator ` R ` at ` ( K + 1 ) ` .
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algrp1 $p |- ( ( ph /\ K e. Z ) ->
                   ( R ` ( K + 1 ) ) = ( F ` ( R ` K ) ) ) $=
      ( wcel wa c1 caddc co cfv fveq1i c1st ccom csn cxp cseq cuz wceq syl6eleq
      simpr seqp1 syl fveq2i fvex algrflem eqtr4i 3eqtr4g ) AFHNZOZFPQRZEUAUBZH
      BUCUDZGUEZSZFVBSZUSVASZUTRZUSCSFCSZESZURFGUFSZNVCVFUGURFHVIAUQUIIUHUTVAGF
      UJUKUSCVBJTVHVDESVFVGVDEFCVBJTULVDVEEFVBUMUNUOUP $.
      $( [27-Dec-2014] $) $( [31-Mar-2011] $)
  $}

  ${
    $d k z A $.  $d x F $.  $d k x z I $.  $d k x z R $.  $d k x z S $.
    $d z K $.
    alginv.1 $e |- R = seq 0 ( ( F o. 1st ) , ( NN0 X. { A } ) ) $.
    alginv.2 $e |- F : S --> S $.
    alginv.3 $e |- I Fn S $.
    alginv.4 $e |- ( x e. S -> ( I ` ( F ` x ) ) = ( I ` x ) ) $.
    $( If ` I ` is an invariant of ` F ` , its value is unchanged after any
       number of iterations of ` F ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    alginv $p |- ( ( A e. S /\ K e. NN0 ) ->
                   ( I ` ( R ` K ) ) = ( I ` ( R ` 0 ) ) ) $=
      ( cn0 wcel cfv cc0 wceq wi fveq2 fveq2d eqeq1d vz vk cv c1 caddc co eqidd
      imbi2d wa nn0uz cz 0z a1i id wf algrf ffvelrn sylan eqeq12d vtoclga eqtrd
      algrp1 syl biimprd expcom a2d nn0ind impcom ) GLMBDMZGCNZFNZOCNZFNZPZVIUA
      UCZCNZFNZVMPZQVIVMVMPZQVIUBUCZCNZFNZVMPZQVIVTUDUEUFZCNZFNZVMPZQVIVNQUAUBG
      VOOPZVRVSVIWHVQVMVMWHVPVLFVOOCRSTUHVOVTPZVRWCVIWIVQWBVMWIVPWAFVOVTCRSTUHV
      OWDPZVRWGVIWJVQWFVMWJVPWEFVOWDCRSTUHVOGPZVRVNVIWKVQVKVMWKVPVJFVOGCRSTUHVI
      VMUGVTLMZVIWCWGVIWLWCWGQVIWLUIZWGWCWMWFWBVMWMWFWAENZFNZWBWMWEWNFVIBCDEVTO
      LUJHOUKMVIULUMZVIUNZDDEUOVIIUMZVBSWMWADMZWOWBPZVILDCUOWLWSVIBCDEOLUJHWPWQ
      WRUPLDVTCUQURAUCZENZFNZXAFNZPWTAWADXAWAPZXCWOXDWBXEXBWNFXAWAERSXAWAFRUSKU
      TVCVATVDVEVFVGVH $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

  ${
    $d A k $.  $d C k z $.  $d F z $.  $d N k $.  $d R k z $.  $d S k z $.
    algcvg.1 $e |- F : S --> S $.
    algcvg.2 $e |- R = seq 0 ( ( F o. 1st ) , ( NN0 X. { A } ) ) $.
    algcvg.3 $e |- C : S --> NN0 $.
    algcvg.4 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                 ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvg.5 $e |- N = ( C ` A ) $.
    $( One way to prove that an algorithm halts is to construct a countdown
       function ` C : S --> NN0 ` whose value is guaranteed to decrease for
       each iteration of ` F ` until it reaches ` 0 ` .  That is, if ` X e. S `
       is not a fixed point of ` F ` , then
       ` ( C `` ( F `` X ) ) < ( C `` X ) ` .

       If ` C ` is a countdown function for algorithm ` F ` , the sequence
       ` ( C `` ( R `` k ) ) ` reaches ` 0 ` after at most ` N ` steps, where
       ` N ` is the value of ` C ` for the initial state ` A ` .  (Contributed
       by Paul Chapman, 22-Jun-2011.) $)
    algcvg $p |- ( A e. S -> ( C ` ( R ` N ) ) = 0 ) $=
      ( wcel cfv cc0 cn0 wf wceq fvco3 clt vk nn0uz cz 0z a1i id algrf ffvelrni
      ccom syl5eqel syl2anc sylancr 0nn0 sylancl algr0 fveq2d eqtrd syl6reqr cv
      fco wa wne wbr c1 caddc co wi ffvelrn sylan fveq2 breq12d imbi12d vtoclga
      neeq1d syl peano2nn0 syl2an algrp1 3imtr4d nn0seqcvgd eqtr3d ) BEMZGCDUIZ
      NZGDNCNZOWBPEDQZGPMWDWERWBBDEFOPUBIOUCMWBUDUEZWBUFZEEFQWBHUEZUGZWBGBCNZPL
      EPBCJUHUJPEGCDSUKWBUAWCGWBEPCQWFPPWCQJWJPEPCDUTULWBOWCNZWKGWBWLODNZCNZWKW
      BWFOPMWLWNRWJUMPEOCDSUNWBWMBCWBBDEFOPUBIWGWHUOUPUQLURWBUAUSZPMZVAZWODNZFN
      ZCNZOVBZWTWRCNZTVCZWOVDVEVFZWCNZOVBXEWOWCNZTVCWQWREMZXAXCVGZWBWFWPXGWJPEW
      ODVHVIAUSZFNZCNZOVBZXKXICNZTVCZVGXHAWREXIWRRZXLXAXNXCXOXKWTOXOXJWSCXIWRFV
      JUPZVNXOXKWTXMXBTXPXIWRCVJVKVLKVMVOWQXEWTOWQXEXDDNZCNZWTWBWFXDPMXEXRRWPWJ
      WOVPPEXDCDSVQWQXQWSCWBBDEFWOOPUBIWGWHWIVRUPUQZVNWQXEWTXFXBTXSWBWFWPXFXBRW
      JPEWOCDSVIVKVSVTWA $.
      $( [22-Jun-2011] $)
  $}

  $( Lemma for ~ algcvgb . $)
  algcvgblem $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( ( N =/= 0 -> N < M ) <->
                      ( ( M =/= 0 -> N < M ) /\ ( M = 0 -> N = 0 ) ) ) ) $=
    ( cn0 wcel wa cc0 wne clt wbr wi wceq wn cle cr wb 0re nn0re adantr syl6bbr
    df-ne wo imor ltnle sylancr nn0le0eq0 notbid bitrd anbi2d nne breq1 biimpar
    sylbi syl6bir exp3a jctir sylibr syl5bi nn0ge0 adantl lelttr mp3an1 syl2anr
    ax-1 jaob mpand sylibd imim2d pm3.34 impbid1 con34b imbi12i bitr4i anbi2i
    jcad ) ACDZBCDZEZBFGZBAHIZJZAFGZVSJZVRWAJZEZWBAFKZBFKZJZEVQVTWDVQVTWBWCVTVR
    LZVSUAZVQWBVRVSUBVQWHWBJZVSWBJZEWIWBJVQWJWKVQWHWAVSVQWHWAEWHFAHIZEVSVQWLWAW
    HVQWLWELZWAVQWLAFMIZLZWMVQFNDZANDZWLWOOPVOWQVPAQZRFAUCUDVOWOWMOVPVOWNWEAUEU
    FRUGAFTZSZUHWHVSWLWHWFVSWLOBFUIBFAHUJULUKUMUNVSWAVCUOWHWBVSVDUPUQVQVSWAVRVQ
    VSWLWAVQFBMIZVSWLVPXAVOBURUSVPBNDZWQXAVSEWLJZVOBQWRWPXBWQXCPFBAUTVAVBVEWTVF
    VGVNVRWAVSVHVIWGWCWBWGWFLZWMJWCWEWFVJVRXDWAWMBFTWSVKVLVMS $.
    $( [31-Mar-2011] $)

  ${
    algcvgb.1 $e |- F : S --> S $.
    algcvgb.2 $e |- C : S --> NN0 $.
    $( Two ways of expressing that ` C ` is a countdown function for algorithm
       ` F ` .  The first is used in these theorems.  The second states the
       condition more intuitively as a conjunction: if the countdown function's
       value is currently non-zero, it must decrease at the next step; if it
       has reached zero, it must remain zero at the next step.  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algcvgb $p |- ( X e. S ->
        ( ( ( C ` ( F ` X ) ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) <->
          ( ( ( C ` X ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) /\
            ( ( C ` X ) = 0 -> ( C ` ( F ` X ) ) = 0 ) ) ) ) $=
      ( wcel cfv cn0 cc0 wne clt wbr wi wceq wa wb ffvelrni syl algcvgblem
      syl2anc ) DBGZDAHZIGDCHZAHZIGZUEJKUEUCLMZNUCJKUGNUCJOUEJONPQBIDAFRUBUDBGU
      FBBDCERBIUDAFRSUCUETUA $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d A k m $.  $d C k m z $.  $d F z $.  $d K m $.  $d N k m $.
    $d R k m z $.  $d S k m z $.
    algcvga.1 $e |- F : S --> S $.
    algcvga.2 $e |- R = seq 0 ( ( F o. 1st ) , ( NN0 X. { A } ) ) $.
    algcvga.3 $e |- C : S --> NN0 $.
    algcvga.4 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                  ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvga.5 $e |- N = ( C ` A ) $.
    $( The countdown function ` C ` remains ` 0 ` after ` N ` steps.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    algcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                ( C ` ( R ` K ) ) = 0 ) ) $=
      ( wcel cn0 cfv cc0 wceq wi wbr vm vk cuz ffvelrni syl5eqel cz nn0z cle wa
      eluz1 cv c1 caddc fveq2 fveq2d eqeq1d imbi2d algcvg a1i w3a nn0ge0 adantr
      co cr nn0re zre 0re letr mp3an1 syl2an mpand elnn0z simplbi2 adantl sylan
      syld expcom 3adant1 ancld wf nn0uz 0z id algrf ffvelrn wne neeq1d breq12d
      impr clt imbi12d vtoclga algcvgb simpr syl6bi mpd syl algrp1 sylibrd syl6
      a2d uzind 3expib sylbid com3r ) BENZHONZGHUCPNZGDPZCPZQRZSXFHBCPOMEOBCKUD
      UEZXGXHXFXKXGHUFNZXHXFXKSZSHUGXMXHGUFNZHGUHTZUIXNHGUJXMXOXPXNXFUAUKZDPZCP
      ZQRZSXFHDPZCPZQRZSZXFUBUKZDPZCPZQRZSXFYEULUMVCZDPZCPZQRZSXNUAUBHGXQHRZXTY
      CXFYMXSYBQYMXRYACXQHDUNUOUPUQXQYERZXTYHXFYNXSYGQYNXRYFCXQYEDUNUOUPUQXQYIR
      ZXTYLXFYOXSYKQYOXRYJCXQYIDUNUOUPUQXQGRZXTXKXFYPXSXJQYPXRXICXQGDUNUOUPUQYD
      XMABCDEFHIJKLMURUSXMYEUFNZHYEUHTZUTZXFYHYLYSXFXFYEONZUIZYHYLSYSXFYTYQYRXF
      YTSXMXFYQYRUIYTXFYQYRYTXFXGYQYRYTSXLXGYQUIZYRQYEUHTZYTUUBQHUHTZYRUUCXGUUD
      YQHVAVBXGHVDNZYEVDNZUUDYRUIUUCSZYQHVEYEVFQVDNUUEUUFUUGVGQHYEVHVIVJVKYQUUC
      YTSXGYTYQUUCYEVLVMVNVPVOWIVQVRVSUUAYHYFFPZCPZQRZYLUUAYFENZYHUUJSZXFOEDVTY
      TUUKXFBDEFQOWAJQUFNXFWBUSZXFWCZEEFVTXFIUSZWDOEYEDWEVOUUKUUIQWFZUUIYGWJTZS
      ZUULAUKZFPZCPZQWFZUVAUUSCPZWJTZSUURAYFEUUSYFRZUVBUUPUVDUUQUVEUVAUUIQUVEUU
      TUUHCUUSYFFUNUOZWGUVEUVAUUIUVCYGWJUVFUUSYFCUNWHWKLWLUUKUURYGQWFUUQSZUULUI
      UULCEFYFIKWMUVGUULWNWOWPWQUUAYKUUIQUUAYJUUHCXFBDEFYEQOWAJUUMUUNUUOWRUOUPW
      SWTXAXBXCXDWQXEWP $.
      $( [22-Jun-2011] $)

    $d K z $.  $d N z $.
    algfx.6 $e |- ( z e. S -> ( ( C ` z ) = 0 -> ( F ` z ) = z ) ) $.
    $( If ` F ` reaches a fixed point when the countdown function ` C ` reaches
       ` 0 ` , ` F ` remains fixed after ` N ` steps.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    algfx $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) -> ( R ` K ) = ( R ` N ) ) ) $=
      ( wcel cfv wceq wi cn0 wa vm vk cz cuz ffvelrni syl5eqel nn0z syl cle wbr
      cv uzval eleq2d pm5.32i c1 caddc co fveq2 eqeq1d imbi2d eqidd a1i eluznn0
      crab sylan cc0 nn0uz 0z id wf algrp1 syldan algcvga imp simpl jca ffvelrn
      algrf eqeq12d imbi12d vtoclga 3syl mpd eqtrd biimprd expcom adantl sylbir
      a2d uzind3 sylbi ex com3r ) BEOZHUCOZGHUDPZOZGDPZHDPZQZRWNHSOZWOWNHBCPSME
      SBCKUEUFZHUGUHWOWQWNWTWOWQWNWTRZWOWQTWOGHAUKZUIUJAUCVDZOZTXCWOWQXFWOWPXEG
      AHULZUMUNWNUAUKZDPZWSQZRWNWSWSQZRZWNUBUKZDPZWSQZRWNXMUOUPUQZDPZWSQZRXCUAA
      UBHGXHHQZXJXKWNXSXIWSWSXHHDURUSUTXHXMQZXJXOWNXTXIXNWSXHXMDURUSUTXHXPQZXJX
      RWNYAXIXQWSXHXPDURUSUTXHGQZXJWTWNYBXIWRWSXHGDURUSUTXLWOWNWSVAVBWOXMXEOZTZ
      WNXOXRYDWOXMWPOZTWNXOXRRZRZWOYEYCWOWPXEXMXGUMUNYEYGWOWNYEYFWNYETZXRXOYHXQ
      XNWSYHXQXNFPZXNWNYEXMSOZXQYIQWNXAYEYJXBXMHVCVEZWNBDEFXMVFSVGJVFUCOWNVHVBZ
      WNVIZEEFVJWNIVBZVKVLYHXNCPZVFQZYIXNQZWNYEYPABCDEFXMHIJKLMVMVNYHWNYJTXNEOZ
      YPYQRZYHWNYJWNYEVOYKVPWNSEDVJYJYRWNBDEFVFSVGJYLYMYNVRSEXMDVQVEXDCPZVFQZXD
      FPZXDQZRYSAXNEXDXNQZUUAYPUUCYQUUDYTYOVFXDXNCURUSUUDUUBYIXDXNXDXNFURUUDVIV
      SVTNWAWBWCWDUSWEWFWGWHWIWJWKWLWMWC $.
      $( [22-Jun-2011] $)
  $}

