$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      The greatest common divisor operator
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c gcd $.

  $( Extend the definition of a class to include the greatest common divisor
     operator. $)
  cgcd $a class gcd $.

  ${
    $d n x y z $.
    $( Define the ` gcd ` operator. $)
    df-gcd $a |- gcd = ( x e. ZZ , y e. ZZ |-> if ( ( x = 0 /\ y = 0 ) , 0 ,
             sup ( { n e. ZZ | ( n || x /\ n || y ) } , RR , < ) ) ) $.
  $}

  ${
    $d M n x y $.  $d N n x y $.
    $( The value of the ` gcd ` operator. ` ( M gcd N ) ` is the greatest
       common divisor of ` M ` and ` N ` .  If ` M ` and ` N ` are both ` 0 ` ,
       the result is defined conventionally as ` 0 ` .  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcdval $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) =
              if ( ( M = 0 /\ N = 0 ) , 0 ,
                   sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) ) $=
      ( vx vy cz cv cc0 wceq wa cdivides wbr crab cr clt cif eqeq1 anbi1d breq2
      csup cgcd rabbidv supeq1d ifbieq2d anbi2d df-gcd cc elexi ltso supex ifex
      0cn ovmpt2 ) DEBCFFDGZHIZEGZHIZJZHAGZUNKLZUSUPKLZJZAFMZNOTZPBHIZCHIZJZHUS
      BKLZUSCKLZJZAFMZNOTZPUAVEUQJZHVHVAJZAFMZNOTZPUNBIZURVMVDVPHVQUOVEUQUNBHQR
      VQNVCVOOVQVBVNAFVQUTVHVAUNBUSKSRUBUCUDUPCIZVMVGVPVLHVRUQVFVEUPCHQUEVRNVOV
      KOVRVNVJAFVRVAVIVHUPCUSKSUEUBUCUDDEAUFVGHVLHUGULUHNVKOUIUJUKUM $.
      $( [10-Nov-2013] $) $( [21-Mar-2011] $)
  $}

  $( The value, by convention, of the ` gcd ` operator when both operands are
     0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcd0val $p |- ( 0 gcd 0 ) = 0 $=
    ( vn cc0 cgcd co wceq wa cv cdivides wbr cz crab cr clt csup wcel 0z gcdval
    cif mp2an eqid iftrue eqtri ) BBCDZBBEZUDFZBAGBHIZUFFAJKLMNZRZBBJOZUIUCUHEP
    PABBQSUDUDUHBEBTZUJUEBUGUASUB $.
    $( [21-Mar-2011] $)

  ${
    $d M n $.  $d N n $.
    $( The value of the ` gcd ` operator when at least one operand is nonzero.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    gcdn0val $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( M gcd N ) = sup ( { n e. ZZ | ( n || M /\ n || N ) } , RR , < ) ) $=
      ( cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab clt csup cif gcdval
      cr iffalse sylan9eq ) BDECDEFBGHCGHFZIBCJKUCGALZBMNUDCMNFADOTPQZRUEABCSUC
      GUEUAUB $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A n w x y z $.  $d S w x $.
    gcdcllem1.1 $e |- S = { z e. ZZ | A. n e. A z || n } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem1 $p |- ( ( A C_ ZZ /\ E. n e. A n =/= 0 ) ->
                      ( S =/= (/) /\ E. x e. RR A. y e. S y <_ x ) ) $=
      ( vw cz cv wa cle wbr wral cr c1 wcel cdivides wceq wi wss cc0 wrex c0 1z
      ssel 1dvds syl6 ralrimiv breq1 ralbidv elrab2 biimpri sylancr ne0i adantr
      wne syl neeq1 cbvrexv cabs simprbi adantl simplbi dvdsleabs 3expia sylan2
      cfv imp anassrs com23 ralrimiva ancoms r19.26 pm3.35 ralimi syl2anc fveq2
      sylbir breq2d imbi12d cbvralv ralbii ralcom r19.21v 3bitri sylib cc zssre
      sstr mpan2 sselda recn abscl wb breq2 rcla4edv syldan imim2d ralimdva mpd
      3syl r19.23v sylan2b jca ) DIUAZFJZUBUQZFDUCZKEUDUQZBJZAJZLMZBENZAOUCZXFX
      JXIXFPEQZXJXFPIQZPXGRMZFDNZXPUEXFXRFDXFXGDQZXGIQZXRDIXGUFZXGUGUHUIXPXQXSK
      CJZXGRMZFDNZXSCPIEYCPSYDXRFDYCPXGRUJUKGULUMUNEPUOURUPXIXFHJZUBUQZHDUCZXOX
      HYGFHDXGYFUBUSZUTXFYHXOXFYGXOTZHDNZYHXOTXFYGXKYFVAVHZLMZBENZTZHDNZYKXFXHX
      KXGVAVHZLMZTZFDNZBENZYPXFYTBEXFXKEQZKXKXGRMZFDNZUUCYSTZFDNZYTUUBUUDXFUUBX
      KIQZUUDYEUUDCXKIEYCXKSYDUUCFDYCXKXGRUJUKGULZVBVCUUBXFUUGUUFUUBUUGUUDUUHVD
      UUGXFUUFUUGXFKZUUEFDUUIXTKXHUUCYRUUGXFXTXHUUCYRTZTZXFXTKUUGYAUUKXFXTYAYBV
      IUUGYAXHUUJXKXGVEVFVGVJVKVLVMVGUUDUUFKUUCUUEKZFDNYTUUCUUEFDVNUULYSFDUUCYS
      VOVPVSVQVLUUAYGYMTZHDNZBENUUMBENZHDNYPYTUUNBEYSUUMFHDXGYFSZXHYGYRYMYIUUPY
      QYLXKLXGYFVAVRVTWAWBWCUUMBHEDWDUUOYOHDYGYMBEWEWCWFWGXFYOYJHDXFYFDQZKZYNXO
      YGXFUUQYLOQZYNXOTUURYFOQYFWHQUUSXFDOYFXFIOUADOUAWIDIOWJWKWLYFWMYFWNXBXFXN
      YNAYLOXLYLSZXNYNWOXFUUTXMYMBEXLYLXKLWPUKVCWQWRWSWTXAYGXOHDXCWGVIXDXE $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K z $.  $d M n x y z $.  $d N n x y z $.  $d R x y $.  $d S x y $.
    gcdcllem2.1 $e |- S = { z e. ZZ | A. n e. { M , N } z || n } $.
    gcdcllem2.2 $e |- R = { z e. ZZ | ( z || M /\ z || N ) } $.
    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> R = S ) $=
      ( vx cz wcel wa cv cdivides wbr wral wceq breq1 elrab2 breq2 cpr anim12ii
      ralbidv wi prid1g prid2g rcla4v syl2an wo vex elpr biimprcd jaao ralrimiv
      syl5bi impbid1 anbi2d syl5bb anbi12d syl6rbbr eqrdv ) EJKZFJKZLZIBCVDIMZC
      KZVEJKZVEENOZVEFNOZLZLZVEBKVFVGVEDMZNOZDEFUAZPZLVDVKAMZVLNOZDVNPVOAVEJCVP
      VEQZVQVMDVNVPVEVLNRUCGSVDVOVJVGVDVOVJVBEVNKZFVNKZVOVJUDVCEFJUEEFJUFVSVOVH
      VTVIVMVHDEVNVLEVENTZUGVMVIDFVNVLFVENTZUGUBUHVJVMDVNVLVNKVLEQZVLFQZUIVJVMV
      LEFDUJUKVHWCVMVIWDWCVMVHWAULWDVMVIWBULUMUOUNUPUQURVPENOZVPFNOZLVJAVEJBVRW
      EVHWFVIVPVEENRVPVEFNRUSHSUTVA $.
      $( [21-Mar-2011] $)

    $( Lemma for ~ gcdn0cl , ~ gcddvds and ~ dvdslegcd . $)
    gcdcllem3 $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
        ( sup ( R , RR , < ) e. NN /\
          ( sup ( R , RR , < ) || M /\ sup ( R , RR , < ) || N ) /\
          ( ( K e. ZZ /\ K || M /\ K || N ) -> K <_ sup ( R , RR , < ) ) ) ) $=
      ( vy vx cz wcel wa cc0 wceq cdivides wbr c1 breq1 wn cr clt cn w3a cle wi
      csup cv crab ssrab2 eqsstri c0 wne wral wrex cpr wss vex elpr eleq1a jaao
      wo syl5bi ssrdv adantr neorian prid1g neeq1 rcla4ev sylan adantlr adantll
      prid2g jaodan sylan2br gcdcllem1 syl2anc wb gcdcllem2 rexbidv anbi12d syl
      raleq mpbird suprzcl mp3an1 sseldi 1dvds anim12i 1z elrab2 mpbiran sylibr
      zssre sstri suprub sylc elnnz1 sylanbrc cbvrabv eqtri sylib biimpri 3impb
      ex simprd syl2im 3jca ) FLMZGLMZNZFOPGOPNUAZNZBUBUCUHZUDMZXOFQRZXOGQRZNZE
      LMZEFQRZEGQRZUEZEXOUFRZUGXNXOLMZSXOUFRZXPXNBLXOBAUIZFQRZYGGQRZNZALUJZLIYJ
      ALUKULZXNBUMUNZJUIKUIZUFRZJBUOZKUBUPZNZXOBMZXNYRCUMUNZYOJCUOZKUBUPZNZXNFG
      UQZLURZDUIZOUNZDUUDUPZUUCXLUUEXMXLKUUDLYNUUDMYNFPZYNGPZVCXLYNLMZYNFGKUSUT
      XJUUIUUKXKUUJFLYNVAGLYNVAVBVDVEVFXMXLFOUNZGOUNZVCUUHFOGOVGXLUULUUHUUMXJUU
      LUUHXKXJFUUDMUULUUHFGLVHUUGUULDFUUDUUFFOVIVJVKVLXKUUMUUHXJXKGUUDMUUMUUHFG
      LVNUUGUUMDGUUDUUFGOVIVJVKVMVOVPKJAUUDCDHVQVRXLYRUUCVSZXMXLBCPZUUNABCDFGHI
      VTUUOYMYTYQUUBBCUMVIUUOYPUUAKUBYOJBCWDWAWBWCVFWEZBLURYMYQYSYLKJBWFWGWCZWH
      XNYRSBMZYFUUPXLUURXMXLSFQRZSGQRZNZUURXJUUSXKUUTFWIGWIWJUURSLMUVAWKYJUVAAS
      LBYGSPYHUUSYIUUTYGSFQTYGSGQTWBIWLWMWNVFBUBURZYMYQUURYFUGBLUBYLWOWPZUVBYMY
      QUEZUURYFKJBSWQXFWGWRXOWSWTXNYEXSXNYSYEXSNUUQYNFQRZYNGQRZNZXSKXOLBYNXOPUV
      EXQUVFXRYNXOFQTYNXOGQTWBBYKUVGKLUJIYJUVGAKLYGYNPYHUVEYIUVFYGYNFQTYGYNGQTW
      BXAXBWLXCXGXNYRYCEBMZYDUUPXTYAYBUVHUVHXTYAYBNZNYJUVIAELBYGEPYHYAYIYBYGEFQ
      TYGEGQTWBIWLXDXEUVBYMYQUVHYDUGUVCUVDUVHYDKJBEWQXFWGXHXI $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K n $.  $d M n z $.  $d N n z $.
    $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdn0cl $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\ -. ( M = 0 /\ N = 0 ) ) ->
                    ( M gcd N ) e. NN ) $=
      ( vn cK vz cz wcel wa cc0 wceq wn cgcd co cv cdivides wbr crab cr cn eqid
      clt csup gcdn0val w3a cle wi cpr wral gcdcllem3 simp1d eqeltrd ) AFGBFGHA
      IJBIJHKHZABLMCNZAOPUMBOPHCFQZRUAUBZSCABUCULUOSGUOAOPUOBOPHDFGDAOPDBOPUDDU
      OUEPUFCUNUMENOPEABUGUHCFQZEDABUPTUNTUIUJUK $.
      $( [21-Mar-2011] $)

    $( The gcd of two integers divides each of them.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    gcddvds $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( ( M gcd N ) || M /\ ( M gcd N ) || N ) ) $=
      ( vn cK vz cz wcel wa cc0 wceq cgcd co cdivides wbr breq1d anbi12d mpbird
      breq2 cv crab 0z dvds0 ax-mp bi2anan9 syl6bb mpbiri oveq12 gcd0val syl6eq
      anidm adantl wn cr clt csup cn w3a cle cpr wral gcdcllem3 simp2d gcdn0val
      wi eqid pm2.61dan ) AFGBFGHZAIJZBIJZHZABKLZAMNZVKBMNZHZVJVNVGVJVNIAMNZIBM
      NZHZVJVQIIMNZIFGVRUAIUBUCVJVQVRVRHVRVHVOVRVIVPVRAIIMRBIIMRUDVRUJUEUFVJVLV
      OVMVPVJVKIAMVJVKIIKLIAIBIKUGUHUIZOVJVKIBMVSOPQUKVGVJULHZVNCSZAMNWABMNHCFT
      ZUMUNUOZAMNZWCBMNZHZVTWCUPGWFDFGDAMNDBMNUQDWCURNVDCWBWAESMNEABUSUTCFTZEDA
      BWGVEWBVEVAVBVTVLWDVMWEVTVKWCAMCABVCZOVTVKWCBMWHOPQVF $.
      $( [21-Mar-2011] $)

    $( An integer which divides both operands of the ` gcd ` operator is
       bounded by it.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdslegcd $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\
                        -. ( M = 0 /\ N = 0 ) ) ->
                      ( ( K || M /\ K || N ) -> K <_ ( M gcd N ) ) ) $=
      ( vn vz cz wcel w3a cc0 wceq wa wn cdivides wbr cle wi cv crab eqid com12
      cgcd co cr clt csup cn cpr gcdcllem3 simp3d gcdn0val breq2d sylibrd 3expb
      wral exp4b com23 impcom 3impb imp ) AFGZBFGZCFGZHBIJCIJKLZABMNZACMNZKZABC
      UAUBZONZPZUTVAVBVCVIPZVAVBKZUTVJVKVCUTVIVKVCUTVFVHUTVFKVKVCKZVHUTVDVEVLVH
      PVLUTVDVEHZVHVLVMADQZBMNVNCMNKDFRZUCUDUEZONZVHVLVPUFGVPBMNVPCMNKVMVQPDVOV
      NEQMNEBCUGUNDFRZEABCVRSVOSUHUIVLVGVPAODBCUJUKULTUMTUOUPUQURUS $.
      $( [21-Mar-2011] $)
  $}

  $( Closure of the ` gcd ` operator.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  gcdcl $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) e. NN0 ) $=
    ( cz wcel wa cc0 wceq cgcd co oveq12 gcd0val syl6eq 0nn0 syl6eqel adantl wn
    cn0 cn gcdn0cl nnnn0 syl pm2.61dan ) ACDBCDEZAFGBFGEZABHIZQDZUDUFUCUDUEFQUD
    UEFFHIFAFBFHJKLMNOUCUDPEUERDUFABSUETUAUB $.
    $( [21-Mar-2011] $)

  ${
    $d n x y $.
    $( Domain and codomain of the ` gcd ` operator.  (Contributed by Paul
       Chapman, 31-Mar-2011.) $)
    gcdf $p |- gcd : ( ZZ X. ZZ ) --> NN0 $=
      ( vx vy vn cv cc0 wceq wa cdivides wbr cz crab clt csup cif cn0 wcel wral
      cr cxp cgcd wf co gcdval gcdcl eqeltrrd rgen2a df-gcd fmpt2 mpbi ) ADZEFB
      DZEFGECDZUJHIULUKHIGCJKRLMNZOPZBJQAJQJJSOTUAUNABJUJJPUKJPGUJUKTUBUMOCUJUK
      UCUJUKUDUEUFABJJUMOTABCUGUHUI $.
      $( [16-Nov-2013] $) $( [31-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The ` gcd ` operator is commutative.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    gcdcom $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd N ) = ( N gcd M ) ) $=
      ( vn cz wcel wa cc0 wceq cv cdivides wbr crab cr clt csup cif cgcd gcdval
      co ancom wb a1i rabbiia supeq1i ifbieq2i ancoms 3eqtr4a ) ADEZBDEZFAGHZBG
      HZFZGCIZAJKZUMBJKZFZCDLZMNOZPUKUJFZGUOUNFZCDLZMNOZPZABQSBAQSZULUSURVBGUJU
      KTMUQVANUPUTCDUPUTUAUMDEUNUOTUBUCUDUECABRUIUHVDVCHCBARUFUG $.
      $( [22-Jun-2011] $) $( [21-Mar-2011] $)
  $}

  $( The gcd of two integers is zero iff they are both zero.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  gcdeq0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( M gcd N ) = 0 <-> ( M = 0 /\ N = 0 ) ) ) $=
    ( cz wcel wa cgcd co cc0 wceq wn wne cn gcdn0cl nnne0 syl ex oveq12 gcd0val
    necon4bd syl6eq impbid1 ) ACDBCDEZABFGZHIAHIBHIEZUBUDUCHUBUDJZUCHKZUBUEEUCL
    DUFABMUCNOPSUDUCHHFGHAHBHFQRTUA $.
    $( [21-Mar-2011] $)

  $( The gcd of two integers is positive (nonzero) iff they are not both zero.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  gcdn0gt0 $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( -. ( M = 0 /\ N = 0 ) <-> 0 < ( M gcd N ) ) ) $=
    ( cz wcel wa cc0 cgcd co clt wbr wne wceq wn cn0 wb gcdcl cle nn0re nn0ge0
    cr 0re leltne mp3an1 syl2anc syl gcdeq0 necon3abid bitr2d ) ACDBCDEZFABGHZI
    JZUJFKZAFLBFLEZMUIUJNDZUKULOZABPUNUJTDZFUJQJZUOUJRUJSFTDUPUQUOUAFUJUBUCUDUE
    UIUMUJFABUFUGUH $.
    $( [22-Jun-2011] $)

  $( The gcd of 0 and an integer is the integer's absolute value.  (Contributed
     by Paul Chapman, 21-Mar-2011.) $)
  gcd0id $p |- ( N e. ZZ -> ( 0 gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co cabs cfv wceq wa cle wbr cdivides 0z mpan adantr nn0z
    wi cn0 syl gcd0val oveq2 fveq2 abs0 syl6eq 3eqtr4a adantl wne gcddvds gcdcl
    simprd dvdsleabs syl3an1 3anidm12 mpd nn0abscl dvds0 iddvds absdvdsb anidms
    wb mpbid jca wn eqid biantrur necon3abii dvdslegcd ex mp3an2 mpancom syl5bi
    w3a imp cr zre letri3 syl2anc mpbir2and pm2.61dane ) ABCZDAEFZAGHZIZADADIZW
    DWAWEDDEFDWBWCUAADDEUBWEWCDGHDADGUCUDUEUFUGWAADUHZJZWDWBWCKLZWCWBKLZWGWBAML
    ZWHWAWJWFWAWBDMLZWJDBCZWAWKWJJNDAUIOUKPWAWFWJWHRZWAWBBCZWAWFWMWAWBSCZWNWLWA
    WONDAUJOWBQTZWBAULUMUNUOWGWCDMLZWCAMLZJZWIWAWSWFWAWQWRWAWCBCZWQWAWCSCWTAUPW
    CQTZWCUQTWAAAMLZWRAURWAXBWRVAAAUSUTVBVCPWAWFWSWIRZWFDDIZWEJZVDZWAXCXEADXDWE
    DVEVFVGWTWAXFXCRZXAWTWLWAXGNWTWLWAVMXFXCWCDAVHVIVJVKVLVNUOWAWDWHWIJVAZWFWAW
    BVOCZWCVOCZXHWAWNXIWPWBVPTWAWTXJXAWCVPTWBWCVQVRPVSVT $.
    $( [21-Mar-2011] $)

  $( The gcd of an integer and 0 is the integer's absolute value.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  gcdid0 $p |- ( N e. ZZ -> ( N gcd 0 ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co cabs cfv wceq 0z gcdcom mpan gcd0id eqtr3d ) ABCZDAEF
    ZADEFZAGHDBCOPQIJDAKLAMN $.
    $( [31-Mar-2011] $)

  $( The gcd of a nonnegative integer with 0 is itself.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  nn0gcdid0 $p |- ( N e. NN0 -> ( N gcd 0 ) = N ) $=
    ( cn0 wcel cc0 cgcd co cabs cfv cz wceq nn0z gcdid0 syl cr cle nn0re nn0ge0
    wbr absid syl2anc eqtrd ) ABCZADEFZAGHZAUBAICUCUDJAKALMUBANCDAORUDAJAPAQAST
    UA $.
    $( [31-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  gcdneg $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M gcd -u N ) = ( M gcd N ) ) $=
    ( cz wcel wa cgcd co cc0 wceq oveq12 adantl syl anbi2d cle cdivides syl2anc
    wb wn wbr wi cneg zcn negeq0 syl6bi imp eqtr4d gcddvds cn0 gcdcl nn0z simpr
    cc dvdsnegb mpbid notbid simpl znegcl w3a dvdslegcd ex syl3anc sylbid com12
    mpdi impcom sylan2 mpbird cr zre letri3 adantr mpbir2and pm2.61dan eqcomd )
    ACDZBCDZEZABFGZABUAZFGZVQAHIZBHIZEZVRVTIZVQWCEVRHHFGZVTWCVRWEIVQAHBHFJKVQWC
    VTWEIZVQWCWAVSHIZEZWFVPWCWHQVOVPWBWGWAVPBULDWBWGQBUBBUCLMKZAHVSHFJUDUEUFVQW
    CRZEWDVRVTNSZVTVRNSZWJVQWKWJVQVRAOSZVRVSOSZEZWKVQWMVRBOSZEWOABUGVQWPWNWMVQV
    RCDZVPWPWNQVQVRUHDWQABUIVRUJLZVOVPUKZVRBUMPMUNVQWJWOWKTZVQWJWHRZWTVQWCWHWIU
    OVQWQVOVSCDZXAWTTWRVOVPUPZVPXBVOBUQZKWQVOXBURXAWTVRAVSUSUTVAVBVCVDVEWJVQWLW
    JVQVTAOSZVTBOSZEZWLVQXGXEVTVSOSZEZVPVOXBXIXDAVSUGVFVQXFXHXEVQVTCDZVPXFXHQVP
    VOXBXJXDVOXBEVTUHDXJAVSUIVTUJLVFZWSVTBUMPMVGVQWJXGWLTZVQXJVOVPWJXLTXKXCWSXJ
    VOVPURWJXLVTABUSUTVAVCVDVEVQWDWKWLEQZWJVQVRVHDZVTVHDZXMVQWQXNWRVRVILVQXJXOX
    KVTVILVRVTVJPVKVLVMVN $.
    $( [21-Mar-2011] $)

  $( Negating one operand of the ` gcd ` operator does not alter the result.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  neggcd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( -u M gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa cneg cgcd co wceq gcdneg ancoms znegcl gcdcom sylan 3eqtr4d )
    ACDZBCDZEBAFZGHZBAGHZRBGHZABGHQPSTIBAJKPRCDQUASIALRBMNABMO $.
    $( [22-Jun-2011] $)

  ${
    gcdaddmlem.1 $e |- K e. ZZ $.
    gcdaddmlem.2 $e |- M e. ZZ $.
    gcdaddmlem.3 $e |- N e. ZZ $.
    $( Lemma for ~ gcdaddm . $)
    gcdaddmlem $p |- ( M gcd N ) = ( M gcd ( ( K x. M ) + N ) ) $=
      ( cc0 wceq cmul co caddc wa cgcd wbr cdivides cz wcel mp2an wi ax-mp cneg
      wn cle gcddvds simpli c1 cn0 gcdcl nn0zi w3a 1z dvds2ln mpanl12 mp3an zcn
      cc mulid2i oveq2i breqtri zmulcl zaddcl dvdslegcd mp2ani mulneg1i oveq12i
      znegcl mulcli negcli addcomi negidi eqtri addassi addid2i 3eqtr3i anim12i
      ex oveq1i zrei letri3i sylibr wo pm4.57 oveq2 mul01i syl6eq oveq1d eqeq1d
      pm5.32i oveq12 sylbir eqtr4d sylbi jaoi pm2.61i ) BGHZABIJZCKJZGHZLZUBZWO
      CGHZLZUBZLZBCMJZBWQMJZHZXDXEXFUCNZXFXEUCNZLXGWTXHXCXIWTXEBONZXEWQONZXHXJX
      ECONZBPQZCPQZXJXLLZEFBCUDRZUEXEWPUFCIJZKJZWQOXOXEXRONZXPXEPQZXMXNXOXSSZXE
      XMXNXEUGQEFBCUHRUIZEFAPQZUFPQZXTXMXNUJYADUKAUFXEBCULUMUNTXQCWPKCXNCUPQFCU
      OTZUQURUSXTXMWQPQZWTXJXKLXHSZSYBEWPPQZXNYFYCXMYHDEABUTRFWPCVARZXTXMYFUJWT
      YGXEBWQVBVPUNVCXCXFBONZXFCONZXIYJXFWQONZXMYFYJYLLZEYIBWQUDRZUEXFAUAZBIJZU
      FWQIJZKJZCOYMXFYRONZYNXFPQZXMYFYMYSSZXFXMYFXFUGQEYIBWQUHRUIZEYIYOPQZYDYTX
      MYFUJUUAYCUUCDAVFTUKYOUFXFBWQULUMUNTYRWPUAZWQKJZCYPUUDYQWQKABYCAUPQDAUOTZ
      XMBUPQEBUOTZVDWQYFWQUPQYIWQUOTUQVEUUDWPKJZCKJGCKJZUUECUUHGCKUUHWPUUDKJGUU
      DWPWPABUUFUUGVGZVHZUUJVIWPUUJVJVKVQUUDWPCUUKUUJYEVLCYEVMZVNVKUSYTXMXNXCYJ
      YKLXISZSUUBEFYTXMXNUJXCUUMXFBCVBVPUNVCVOXEXFXEYBVRXFUUBVRVSVTXDUBWSXBWAXG
      WSXBWBWSXGXBWSXBXGWOWRXAWOWQCGWOWQUUICWOWPGCKWOWPAGIJGBGAIWCAUUFWDWEWFUUL
      WEWGWHZXBXEGGMJZXFBGCGMWIXBWSXFUUOHUUNBGWQGMWIWJWKZWLUUPWMWLWN $.
      $( [31-Mar-2011] $)
  $}

  $( Adding a multiple of one operand of the ` gcd ` operator to the other does
     not alter the result.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdaddm $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                  ( M gcd N ) = ( M gcd ( N + ( K x. M ) ) ) ) $=
    ( cz wcel cgcd co cmul caddc cc0 cif oveq1 oveq1d oveq2d oveq2 0z elimel cc
    wceq zcn eqeq2d id oveq12d eqeq12d gcdaddmlem dedth3h wa mulcl syl2an 3impa
    w3a addcom eqtrd ) ADEZBDEZCDEZUKZBCFGZBABHGZCIGZFGZBCUSIGZFGUNUOUPURVASURB
    UNAJKZBHGZCIGZFGZSUOBJKZCFGZVGVCVGHGZCIGZFGZSVGUPCJKZFGZVGVIVLIGZFGZSABCJJJ
    AVCSZVAVFURVPUTVEBFVPUSVDCIAVCBHLMNUABVGSZURVHVFVKBVGCFLVQBVGVEVJFVQUBVQVDV
    ICIBVGVCHOMUCUDCVLSZVHVMVKVOCVLVGFOVRVJVNVGFCVLVIIONUDVCVGVLAJDPQBJDPQCJDPQ
    UEUFUQUTVBBFUNUOUPUTVBSZUNUOUGUSREZCREVSUPUNAREBREVTUOATBTABUHUICTUSCULUIUJ
    NUM $.
    $( [31-Mar-2011] $)

  $( The GCD of two numbers is the same as the GCD of the left and their sum.
     (Contributed by Scott Fenton, 20-Apr-2014.) $)
  gcdadd $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
  ( M gcd N ) = ( M gcd ( N + M ) ) ) $=
    ( cz wcel wa cgcd co c1 cmul caddc wceq 1z gcdaddm mp3an1 zcn mulid2 oveq2d
    cc syl adantr eqtrd ) ACDZBCDZEABFGZABHAIGZJGZFGZABAJGZFGZHCDUBUCUDUGKLHABM
    NUBUGUIKZUCUBARDZUJAOUKUFUHAFUKUEABJAPQQSTUA $.
    $( [20-Apr-2014] $)

  $( The gcd of a number and itself is its absolute value.  (Contributed by
     Paul Chapman, 31-Mar-2011.) $)
  gcdid $p |- ( N e. ZZ -> ( N gcd N ) = ( abs ` N ) ) $=
    ( cz wcel cc0 cgcd co c1 cmul caddc cabs cfv wceq 1z gcdaddm mp3an13 gcdid0
    0z cc zcn oveq2d mulid2 addid2 eqtrd syl 3eqtr3rd ) ABCZADEFZADGAHFZIFZEFZA
    JKAAEFGBCUFDBCUGUJLMQGADNOAPUFUIAAEUFARCZUIALASUKUIDAIFAUKUHADIAUATAUBUCUDT
    UE $.
    $( [31-Mar-2011] $)
  $( The gcd of a number with 1 is 1.  (Contributed by Mario Carneiro,
     19-Feb-2014.) $)
  gcd1 $p |- ( M e. ZZ -> ( M gcd 1 ) = 1 ) $=
    ( cz wcel c1 cgcd co cle wbr wceq cdivides wa 1z gcddvds mpan2 simprd cn wi
    cc0 wn syl wne ax-1ne0 simpr necon3ai gcdn0cl nnz 1nn dvdsle sylancl mpd wb
    ax-mp nnle1eq1 mpbid ) ABCZADEFZDGHZUPDIZUOUPDJHZUQUOUPAJHZUSUODBCZUTUSKLAD
    MNOUOUPBCZDPCUSUQQUOUPPCZVBUOVAVCLUOVAKARIZDRIZKZSZVCDRUAVGUBVFDRVDVEUCUDUL
    ADUENNZUPUFTUGUPDUHUIUJUOVCUQURUKVHUPUMTUN $.
    $( [19-Feb-2014] $)

  $( The gcd of two integers is the same as that of their absolute values.
     (Contributed by Paul Chapman, 31-Mar-2011.) $)
  gcdabs $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                 ( ( abs ` M ) gcd ( abs ` N ) ) = ( M gcd N ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo co cr zre absor anim12i oveq12 sylan9eqr
    cgcd ex gcdneg syl2an wi a1i neggcd znegcl sylan eqtrd ccased mpd ) ACDZBCD
    ZEZAFGZAHZUMAIZHZJZBFGZBHZURBIZHZJZEZUMURRKZABRKZHZUJALDZBLDZVCUKAMBMVGUQVH
    VBANBNOUAULUNUSUPVAVFUNUSEVFUBULUMAURBRPUCULUPUSEZVFVIULVDUOBRKZVEUMUOURBRP
    ABUDZQSULUNVAEZVFVLULVDAUTRKVEUMAURUTRPABTQSULUPVAEZVFVMULVDUOUTRKZVEUMUOUR
    UTRPULVNVJVEUJUOCDUKVNVJHAUEUOBTUFVKUGQSUHUI $.
    $( [31-Mar-2011] $)

  $( ` gcd ` of the absolute value of the first operator.  (Contributed by
     Scott Fenton, 02-Apr-2014.) $)
  gcdabs1 $p |- ( ( N e. ZZ /\ M e. ZZ ) ->
    ( ( abs ` N ) gcd M ) = ( N gcd M ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo cgcd co cr zre absor syl adantr wi oveq1
    a1i neggcd eqeq1d syl5ibrcom jaod mpd ) BCDZACDZEZBFGZBHZUIBIZHZJZUIAKLZBAK
    LZHZUFUMUGUFBMDUMBNBOPQUHUJUPULUJUPRUHUIBAKSTUHUPULUKAKLZUOHBAUAULUNUQUOUIU
    KAKSUBUCUDUE $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( ` gcd ` of the absolute value of the second operator.  (Contributed by
     Scott Fenton, 02-Apr-2014.) $)
  gcdabs2 $p |- ( ( N e. ZZ /\ M e. ZZ ) ->
    ( N gcd ( abs ` M ) ) = ( N gcd M ) ) $=
    ( cz wcel wa cabs cfv cgcd wceq gcdabs1 ancoms cn0 nn0abscl nn0z syl gcdcom
    co sylan2 3eqtr4d ) BCDZACDZEAFGZBHQZABHQZBUBHQZBAHQUATUCUDIBAJKUATUBCDZUEU
    CIUAUBLDUFAMUBNOBUBPRBAPS $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( The gcd remains unchanged if one operand is replaced with its remainder
     modulo the other.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
  modgcd $p |- ( ( M e. ZZ /\ N e. NN ) ->
                 ( ( M mod N ) gcd N ) = ( M gcd N ) ) $=
    ( cz wcel wa co cgcd cneg cmul caddc cr wceq cc zcn adantl syl eqtrd oveq2d
    syl3anc eqtr4d cmo cdiv cfl cfv cmin crp zre nnrp modval syl2an adantr nncn
    cn cc0 wne nnre nnne0 redivcl syl3an 3anidm23 flcl w3a mulcom negeqd ancoms
    mulneg1 3adant1 mulcl negsub sylan2 3impb znegcl nnz simpl gcdaddm cn0 nn0z
    zmodcl gcdcom syl2anc 3eqtr3d ) ACDZBUMDZEZBABUAFZGFZBAGFZWEBGFZABGFZWDWFBA
    ABUBFZUCUDZHZBIFZJFZGFZWGWDWEWNBGWDWEABWKIFZUEFZWNWBAKDZBUFDWEWQLWCAUGZBUHA
    BUIUJWDAMDZBMDZWKMDZWNWQLWBWTWCANUKWCXAWBBULOWDWKCDZXBWDWJKDZXCWBWCXDWBWRWC
    BKDWCBUNUOXDWSBUPBUQABURUSUTWJVAPZWKNPWTXAXBVBZWNAWPHZJFZWQXFWMXGAJXAXBWMXG
    LZWTXBXAXIXBXAEZWMWKBIFZHXGWKBVFXJXKWPWKBVCVDQVEVGRWTXAXBXHWQLZXAXBEWTWPMDX
    LBWKVHAWPVIVJVKQSTRWDWLCDZBCDZWBWGWOLWDXCXMXEWKVLPWCXNWBBVMOZWBWCVNZWLBAVOS
    TWDXNWECDZWFWHLXOWDWEVPDXQABVRWEVQPBWEVSVTWDXNWBWGWILXOXPBAVSVTWA $.
    $( [31-Mar-2011] $)

  $( The GCD of one and an integer is one.  (Contributed by Scott Fenton,
     17-Apr-2014.) $)
  1gcd $p |- ( M e. ZZ -> ( 1 gcd M ) = 1 ) $=
    ( cz wcel c1 cgcd co wceq 1z gcdcom mpan gcd1 eqtrd ) ABCZDAEFZADEFZDDBCMNO
    GHDAIJAKL $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

