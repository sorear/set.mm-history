$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           The division algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    divalglem0.1 $e |- N e. ZZ $.
    divalglem0.2 $e |- D e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem0 $p |- ( ( R e. ZZ /\ K e. ZZ ) -> ( D || ( N - R ) ->
                       D || ( N - ( R - ( K x. ( abs ` D ) ) ) ) ) ) $=
      ( cz wcel wa cmin co cdivides wbr cabs ax-mp wi mp3an1 syl2an cc zcn cmul
      cfv caddc iddvds wb dvdsabsb anidms mpbid cn0 nn0abscl dvdsmultr2 mp3an13
      nn0zi mpi adantl zsubcl mpan zmulcl mpan2 dvds2add mpan2d wceq syl subsub
      breq2d sylibrd ) BGHZCGHZIZADBJKZLMZAVJCANUBZUAKZUCKZLMZADBVMJKJKZLMVIVKA
      VMLMZVOVHVQVGVHAVLLMZVQAGHZVRFVSAALMZVRAUDVSVTVRUEAAUFUGUHOVSVHVLGHZVRVQP
      FVLVSVLUIHFAUJOUMZACVLUKULUNUOVGVJGHZVMGHZVKVQIVOPZVHDGHZVGWCEDBUPUQVHWAW
      DWBCVLURUSZVSWCWDWEFAVJVMUTQRVAVIVPVNALVGBSHZVMSHZVPVNVBZVHBTVHWDWIWGVMTV
      CDSHZWHWIWJWFWKEDTODBVMVDQRVEVF $.
      $( [21-Mar-2011] $)

    divalglem1.3 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem1 $p |- 0 <_ ( N + ( abs ` ( N x. D ) ) ) $=
      ( cmul co cabs cfv cle wbr cc0 zrei 0re wcel ax-mp cr wb recni abscli wne
      cneg caddc wo letrii c1 cn nnabscl mp2an nnge1 le0neg1 renegcli lemulge11
      cz wa mpanl12 sylanb mpan2 absmuli absnidi oveq1d syl5eq breqtrrd le0neg2
      remulcli absge0i letri sylbi jaoi cmin df-neg breq1i lesubadd2i bitri
      mpbi ) BUBZBAFGZHIZJKZLBVRUCGJKZBLJKZLBJKZUDVSBLBCMZNUEWAVSWBWAVPVPAHIZFG
      ZVRJWAUFWDJKZVPWEJKZWDUGOZWFAUNOALUAWHDEAUHUIWDUJPWALVPJKZWFWGBQOZWAWIRWC
      BUKPVPQOWDQOWIWFUOWGBWCULZAAADMZSZTVPWDUMUPUQURWAVRBHIZWDFGWEBABWCSWMUSWA
      WNVPWDFBWCUTVAVBVCWBVPLJKZVSWJWBWORWCBVDPWOLVRJKVSVQVQBAWCWLVESZVFVPLVRWK
      NVQWPTZVGURVHVIPVSLBVJGZVRJKVTVPWRVRJBVKVLLBVRNWCWQVMVNVO $.
      $( [21-Mar-2011] $)

    $d D r $.  $d N r $.
    divalglem2.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem2 $p |- sup ( S , RR , `' < ) e. S $=
      ( cc0 cfv wcel cn0 cmin co cdivides wbr cmul cabs cz ax-mp cuz wss c0 wne
      cr ccnv csup cv crab ssrab2 eqsstri nn0uz sseqtri caddc wceq oveq2 breq2d
      elrab2 elnn0z zmulcl mp2an nn0abscl nn0zi zaddcl divalglem1 mpbir2an cneg
      clt cle iddvds wb dvdsabsb anidms mpbid nn0negzi dvdsmultr2 mp3an absmuli
      wi cc negeqi df-neg subidi oveq1i nn0cni subsub4 3eqtr2ri abscli mulneg1i
      zcn recni 3eqtr4i breqtrri ne0i infmssuzcl ) BIUAJZUBBUCUDZBUEVHUFUGBKBLW
      PBACDUHZMNZOPZDLUILHWTDLUJUKULUMCCAQNZRJZUNNZBKZWQXDXCLKZACXCMNZOPZWTXGDX
      CLBWRXCUOWSXFAOWRXCCMUPUQHURXEXCSKZIXCVIPXCUSCSKZXBSKXHEXBXASKZXBLKXIASKZ
      XJEFCAUTVAXAVBTZVCCXBVDVAACEFGVEVFACRJZVGZARJZQNZXFOAXOOPZAXPOPZXKXQFXKAA
      OPZXQAVJXKXSXQVKAAVLVMVNTXKXNSKXOSKXQXRVSFXMXIXMLKECVBTVOXOXKXOLKFAVBTVCA
      XNXOVPVQTXBVGZXMXOQNZVGXFXPXBYACAXICVTKZECWJTZXKAVTKFAWJTZVRWAXTIXBMNCCMN
      ZXBMNZXFXBWBYEIXBMCYCWCWDYBYBXBVTKYFXFUOYCYCXBXLWECCXBWFVQWGXMXOXMCYCWHWK
      XOAYDWHWKWIWLWMVFBXCWNTBIWOVA $.
      $( [21-Mar-2011] $)

    $d D q r x z $.  $d N q r x z $.  $d R x $.  $d S z $.
    $( Lemma for ~ divalg . $)
    divalglem4 $p |- S = { r e. NN0 | E. q e. ZZ N = ( ( q x. D ) + r ) } $=
      ( cv co caddc wceq cz wrex cn0 wcel cmin cdivides wa vz cmul crab wb nn0z
      wbr zsubcl mpan divides cc nn0cn zmulcl mpan2 zcn syl ax-mp subadd mp3an1
      3syl addcom eqeq1d bitrd syl2an eqcom 3bitr3g pm5.32i oveq2 breq2d elrab2
      rexbidva eqeq2d rexbidv elrab 3bitr4i eqriv ) UABCEJZAUBKZDJZLKZMZENOZDPU
      CZUAJZPQZACWCRKZSUFZTWDCVQWCLKZMZENOZTWCBQWCWBQWDWFWIWDWFVQWEMZENOZWIWDWC
      NQZWENQZWFWKUDZWCUECNQZWLWMFCWCUGUHANQZWMWNGEAWEUIUHUSWDWJWHENWDVPNQZTWEV
      QMZWGCMZWJWHWDWCUJQZVQUJQZWRWSUDWQWCUKWQVQNQZXAWQWPXBGVPAULUMVQUNUOWTXATZ
      WRWCVQLKZCMZWSCUJQZWTXAWRXEUDWOXFFCUNUPCWCVQUQURXCXDWGCWCVQUTVAVBVCWEVQVD
      WGCVDVEVJVBVFACVRRKZSUFWFDWCPBVRWCMZXGWEASVRWCCRVGVHIVIWAWIDWCPXHVTWHENXH
      VSWGCVRWCVQLVGVKVLVMVNVO $.
      $( [21-Mar-2011] $)

    divalglem5.5 $e |- R = sup ( S , RR , `' < ) $.
    $( Lemma for ~ divalg . $)
    divalglem5 $p |- ( 0 <_ R /\ R < ( abs ` D ) ) $=
      ( vx cc0 cle wbr cn0 wcel cmin co cdivides wceq cabs cfv clt wa ccnv csup
      cr divalglem2 eqeltri oveq2 breq2d crab cbvrabv eqtri elrab2 mpbi nn0ge0i
      cv simpli wn cz wne cn nnabscl mp2an nngt0i 0re zcn abscli ltnlei cuz wss
      cc ax-mp ssrab2 eqsstri nn0uz sseqtri nn0abscl nn0sub2 mp3an12 wi nn0z c1
      a1i cmul divalglem0 mpan2 recni mulid2i oveq2i breq2i syl6ib syl sylanbrc
      1z infmssuzle sylancr syl5eqbr wb simpld nn0re lesub mp3an3 syl2anc recnd
      imp subid bitrd mpbid mto mpbir pm3.2i ) LBMNBAUAUBZUCNZBBOPZADBQRZSNZBCP
      XPXRUDZBCUGUCUEUFZCJACDEFGHIUHUIADKURZQRZSNZXRKBOCYABTYBXQASYABDQUJUKCADE
      URZQRZSNZEOULZYCKOULIYFYCEKOYDYATYEYBASYDYADQUJUKUMUNZUOUPZUSZUQXOXNBMNZU
      TYKXNLMNZLXNUCNYLUTXNAVAPZALVBXNVCPGHAVDVEVFLXNVGAYMAVMPGAVHVNVIZVJUPYKBB
      XNQRZMNZYLYKBXTYOMJYKCLVKUBZVLYOCPZXTYOMNCOYQCYGOIYFEOVOVPVQVRYKYOOPZADYO
      QRZSNZYRXNOPZXPYKYSYMUUBGAVSVNYJXNBVTWAYKXSUUAXSYKYIWEZXPXRUUAXPBVAPZXRUU
      AWBBWCUUDXRADBWDXNWFRZQRZQRZSNZUUAUUDWDVAPXRUUHWBWPABWDDFGWGWHUUGYTASUUFY
      ODQUUEXNBQXNXNYNWIWJWKWKWLWMWNXGWNYCUUAKYOOCYAYOTYBYTASYAYODQUJUKYHUOWOYO
      CLWQWRWSYKYPXNBBQRZMNZYLYKBUGPZUUKYPUUJWTZYKXPUUKYKXPXRUUCXABXBZWNZUUNUUK
      UUKXNUGPUULYNBBXNXCXDXEYKUUILXNMYKBVMPUUILTYKBUUNXFBXHWNUKXIXJXKBXNXPUUKY
      JUUMVNYNVJXLXM $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem6.1 $e |- A e. NN $.
    divalglem6.2 $e |- X e. ( 0 ... ( A - 1 ) ) $.
    divalglem6.3 $e |- K e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem6 $p |- ( K =/= 0 ->
                       -. ( X + ( K x. A ) ) e. ( 0 ... ( A - 1 ) ) ) $=
      ( cc0 clt wbr co caddc c1 cmin wcel cle cz wb 0z mp2an cr wne wo cmul cfz
      wn zrei 0re lttri2i w3a nnzi elfzm11 simp3i simp1i nnrei remulcli ltadd1i
      mpbi cneg renegcli wa nnnn0i nn0ge0i lemulge12 an4s mpanl12 lt0neg1 ax-mp
      znegcl zltp1le ax-1cn addid2i breq1i bitri mulneg1i oveq2i subnegi suble0
      mpan recni eqtri bitr3i 3imtr4i ltletri sylancr ltnlei sylib elfzle1 nsyl
      readdcli sylbi simp2i addge02 letri sylancl lenlti simp3bi jaoi ) BGUABGH
      IZGBHIZUBCBAUCJZKJZGALMJZUDJZNZUEZBGBFUFZUGUHWRXEWSWRGXAOIZXDWRXAGHIZXGUE
      WRXAAWTKJZHIZXIGOIZXHCAHIZXJCPNZGCOIZXLCXCNZXMXNXLUIZEGPNZAPNZXOXPQRADUJZ
      CGAUKSUQZULCAWTCXMXNXLXTUMUFZADUNZBAXFYBUOZUPUQLBURZOIZAYDAUCJZOIZWRXKYDT
      NZYEYGBXFUSZATNZGAOIZYHYEUTYGYBAADVAVBZYJYHYKYEYGAYDVCVDVEVRWRGYDHIZYEBTN
      ZWRYMQXFBVFVGYMGLKJZYDOIZYEXQYDPNZYMYPQRBPNZYQFBVHVGGYDVISYOLYDOLVJVKZVLV
      MVMXKAYFMJZGOIZYGYTXIGOYTAWTURZMJXIYFUUBAMBABXFVSAYBVSZVNVOAWTUUCWTYCVSVP
      VTVLYJYFTNUUAYGQYBYDAYIYBUOAYFVQSWAWBXAXIGCWTYAYCWIZAWTYBYCWIUGWCWDXAGUUD
      UGWEWFXAGXBWGWHWSXAAHIZXDWSAXAOIZUUEUEWSAWTOIZWTXAOIZUUFWSLBOIZUUGWSYOBOI
      ZUUIXQYRWSUUJQRFGBVISYOLBOYSVLVMYKUUIUUGYLYJYNYKUUIUTUUGYBXFABVCVEVRWJXNU
      UHXMXNXLXTWKWTTNCTNXNUUHQYCYAWTCWLSUQAWTXAYBYCUUDWMWNAXAYBUUDWOWFXDXAPNZX
      GUUEXQXRXDUUKXGUUEUIQRXSXAGAUKSWPWHWQWJ $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem7.1 $e |- D e. ZZ $.
    divalglem7.2 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem7 $p |- ( ( X e. ( 0 ... ( ( abs ` D ) - 1 ) ) /\ K e. ZZ ) ->
                       ( K =/= 0 -> -. ( X + ( K x. ( abs ` D ) ) ) e.
                                       ( 0 ... ( ( abs ` D ) - 1 ) ) ) ) $=
      ( cc0 co wcel cz wne cmul caddc wn wi cif wceq oveq1 eleq1d notbid 0z cfv
      cabs c1 cmin cfz imbi2d neeq1 oveq2d imbi12d cn nnabscl mp2an cle wbr clt
      w3a wb nnzi elfzm11 0re leidi nngt0i mpbir3an elimel divalglem6 dedth2h )
      CFAUBUAZUCUDGUEGZHZBIHZBFJZCBVGKGZLGZVHHZMZNVKVICFOZVLLGZVHHZMZNVJBFOZFJZ
      VPVTVGKGZLGZVHHZMZNCBFFCVPPZVOVSVKWFVNVRWFVMVQVHCVPVLLQRSUFBVTPZVKWAVSWEB
      VTFUGWGVRWDWGVQWCVHWGVLWBVPLBVTVGKQUHRSUIVGVTVPAIHAFJVGUJHDEAUKULZCFVHFVH
      HZFIHZFFUMUNZFVGUOUNZWJVGIHWIWJWKWLUPUQTVGWHURFFVGUSULTFUTVAVGWHVBVCVDBFI
      TVDVEVF $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r x z $.  $d N q r x z $.  $d S x z $.  $d X z $.  $d Y z $.
    divalglem8.1 $e |- N e. ZZ $.
    divalglem8.2 $e |- D e. ZZ $.
    divalglem8.3 $e |- D =/= 0 $.
    divalglem8.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem8 $p |-
        ( ( ( X e. S /\ Y e. S ) /\ ( X < ( abs ` D ) /\ Y < ( abs ` D ) ) ) ->
          ( K e. ZZ -> ( ( K x. ( abs ` D ) ) = ( Y - X ) -> X = Y ) ) ) $=
      ( wcel cz co wceq wi wa cc0 cc cn0 vz cabs cfv clt wbr cmul cmin caddc wb
      w3a cv cdivides crab ssrab2 eqsstri nn0sscn sstri sseli wne nnabscl mp2an
      cn nnzi zmulcl mpan2 zcn syl subadd syl3an 3com12 eqcom 3adant1r 3adant2r
      3bitr3g wn cfz breq1 eleq1 imbi12d cle elnn0z sylib anim1i df-3an elfzm11
      c1 sylibr 0z vtoclga biimpd sylan9 impancom 3ad2ant2 imp divalglem7 sylan
      3adant2 con2d syld df-ne con2bii syl6ibr sylbid oveq1 nncni mul02i syl6eq
      ex eqeq1d biimpac subeq0 syl2anr syl5ib ad2ant2r 3adant3 mpdd 3expia an4s
      exp3a ) EBLZEAUBUCZUDUEZFBLZFYAUDUEZCMLZCYAUFNZFEUGNZOZEFOZPZPXTYBQZYCYDQ
      ZYEYJYKYLYEUJZYHCROZYIYMYHFEYFUHNZOZYNYKYCYEYHYPUIZYDXTYCYEYQYBXTYCYEUJYG
      YFOZYOFOZYHYPYCXTYEYRYSUIZYCFSLZXTESLZYEYFSLZYTBSFBTSBADGUKUGNULUEZGTUMTK
      UUDGTUNUOZUPUQZURZBSEUUFURZYEYFMLZUUCYEYAMLZUUIYAAMLARUSYAVBLIJAUTVAZVCZC
      YAVDVEYFVFVGFEYFVHVIVJYGYFVKYOFVKVNVLVMYMYPCRUSZVOZYNYMYPYORYAWFUGNVPNZLZ
      UUNYLYKYPUUPPYEYCYPYDUUPYCYDFUUOLZYPUUPUAUKZYAUDUEZUURUUOLZPZYDUUQPUAFBUU
      RFOUUSYDUUTUUQUURFYAUDVQUURFUUOVRVSUURBLZUUSUUTUVBUUSQZUURMLZRUURVTUEZUUS
      UJZUUTUVCUVDUVEQZUUSQUVFUVBUVGUUSUVBUURTLUVGBTUURUUEURUURWAWBWCUVDUVEUUSW
      DWGRMLUUJUUTUVFUIWHUULUURRYAWEVAWGXHZWIYPUUQUUPFYOUUOVRWJWKWLWMYMUUMUUPYK
      YEUUMUUPVOPZYLYKEUUOLZYEUVIXTYBUVJUVAYBUVJPUAEBUUREOUUSYBUUTUVJUUREYAUDVQ
      UUREUUOVRVSUVHWIWNACEIJWOWPWQWRWSUUMYNCRWTXAXBXCYMYHYNYIYKYLYHYNQZYIPZYEX
      TYCUVLYBYDUVKRYGOZXTYCQZYIYNYHUVMYNYFRYGYNYFRYAUFNRCRYAUFXDYAYAUUKXEXFXGX
      IXJUVNYGROZFEOZUVMYIYCUUAUUBUVOUVPUIXTUUGUUHFEXKXLYGRVKFEVKVNXMXNXOXSXPXQ
      XR $.
      $( [21-Mar-2011] $)

    ${
      $d D k r x y $.  $d R x $.  $d S k x y $.
      divalglem9.5 $e |- R = sup ( S , RR , `' < ) $.
      $( Lemma for ~ divalg . $)
      divalglem9 $p |- E! x e. S x < ( abs ` D ) $=
        ( wbr wa wceq wcel cc0 co cmin cz cdivides vy vk cabs cfv clt wreu wrex
        cv wi wral breq1 reu4 cr ccnv csup divalglem2 eqeltri divalglem5 simpri
        cle rcla4ev mp2an cmul cn0 oveq2 breq2d elrab2 simplbi nn0z zsubcl mpan
        syl anim12i syl2an simprbi dvds2sub mp3an1 sylc cc zcn caddc zrei recni
        subidi oveq1i 0cn subsub2 syl5eq sub4 mpanl12 subcl ancoms addid2 mpbid
        3eqtr3d wb absdvdsb sylancr wne cn nnabscl nnzi divides adantr rexlimdv
        divalglem8 mpd ex rgen2a mpbir2an ) AUHZBUCUDZUELZADUFXMADUGZXMUAUHZXLU
        ELZMZXKXONZUIZUADUJADUJXMXPAUADXKXOXLUEUKULCDOCXLUELZXNCDUMUEUNUODKBDEF
        GHIJUPUQPCUTLXTBCDEFGHIJKURUSXMXTACDXKCXLUEUKVAVBXSAUADXKDOZXODOZMZXQXR
        YCXQMZUBUHZXLVCQXOXKRQZNZUBSUGZXRYCYHXQYCXLYFTLZYHYCBYFTLZYIYCBEXKRQZEX
        ORQZRQZTLZYJYCYKSOZYLSOZMZBYKTLZBYLTLZMZYNYAXKSOZXOSOZYQYBYAXKVDOZUUAYA
        UUCYRBEFUHZRQZTLZYRFXKVDDUUDXKNUUEYKBTUUDXKERVEVFJVGZVHXKVIVLZYBXOVDOZU
        UBYBUUIYSUUFYSFXOVDDUUDXONUUEYLBTUUDXOERVEVFJVGZVHXOVIVLZUUAYOUUBYPESOZ
        UUAYOGEXKVJVKUULUUBYPGEXOVJVKVMVNYAYRYBYSYAUUCYRUUGVOYBUUIYSUUJVOVMBSOZ
        YOYPYTYNUIHBYKYLVPVQVRYCYMYFBTYAUUAUUBYMYFNZYBUUHUUKUUAXKVSOZXOVSOZUUNU
        UBXKVTXOVTUUOUUPMZEERQZXKXORQZRQZPYFWAQZYMYFUUQUUTPUUSRQZUVAUURPUUSREEE
        GWBWCZWDWEPVSOUUOUUPUVBUVANWFPXKXOWGVQWHEVSOZUVDUUQUUTYMNUVCUVCEEXKXOWI
        WJUUQYFVSOZUVAYFNUUPUUOUVEXOXKWKWLYFWMVLWOVNVNVFWNYAUUAUUBYJYIWPZYBUUHU
        UKUUAUUBMZUUMYFSOZUVFHUUBUUAUVHXOXKVJWLZBYFWQWRVNWNYAUUAUUBYIYHWPZYBUUH
        UUKUVGXLSOUVHUVJXLUUMBPWSXLWTOHIBXAVBXBUVIUBXLYFXCWRVNWNXDYDYGXRUBSBDYE
        EXKXOFGHIJXFXEXGXHXIXJ $.
        $( [21-Mar-2011] $)
    $}

    $( Lemma for ~ divalg . $)
    divalglem10 $p |- E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) $=
      ( vx cc0 cv wbr clt caddc wceq cz wrex wa anbi2i cle cabs cfv cmul co w3a
      wreu cr ccnv csup eqid divalglem9 weu cn0 oveq2 eqeq2d rexbidv divalglem4
      wcel elrab2 ancom 3bitr4i elnn0z an12 3bitri anbi1i df-3an rexbii r19.42v
      anass bitri bitr4i eubii df-reu mpbi breq2 breq1 3anbi123d cbvreuv ) KJLZ
      UAMZVTAUBUCZNMZCELAUDUEZVTOUEZPZUFZEQRZJQUGZKDLZUAMZWJWBNMZCWDWJOUEZPZUFZ
      EQRZDQUGWCJBUGZWIJABUHNUIUJZBCDFGHIWRUKULVTBUSZWCSZJUMVTQUSZWHSZJUMWQWIWT
      XBJWTXAWAWCSZWFEQRZSZSZXBWTWCVTUNUSZSZXDSZXAXCSZXDSXFWCWSSWCXGXDSZSWTXIWS
      XKWCWNEQRXDDVTUNBWJVTPZWNWFEQXLWMWECWJVTWDOUOUPUQABCDEFGHIURUTTWSWCVAWCXG
      XDVJVBXHXJXDXHWCXAWASZSXAWCWASZSXJXGXMWCVTVCTWCXAWAVDXNXCXAWCWAVATVEVFXAX
      CXDVJVEWHXEXAWHXCWFSZEQRXEWGXOEQWAWCWFVGVHXCWFEQVIVKTVLVMWCJBVNWHJQVNVBVO
      WHWPJDQVTWJPZWGWOEQXPWAWKWCWLWFWNVTWJKUAVPVTWJWBNVQXPWEWMCVTWJWDOUOUPVRUQ
      VSVO $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r $.  $d N q r $.
    $( The division algorithm (theorem).  Dividing an integer ` N ` by a
       nonzero integer ` D ` produces a (unique) quotient ` q ` and a unique
       remainder ` 0 <_ r < ( abs `` D ) ` .  The proof does not use ` / ` ,
       ` |_ ` or ` mod ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divalg $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) -> E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) ) $=
      ( cz wcel cc0 wne cv wbr cabs clt cmul co caddc wceq w3a wrex wreu c1 cle
      cfv wa cif eqeq1 3anbi3d rexbidv reubidv fveq2 breq2d oveq2 oveq1d eqeq2d
      3anbi23d cmin cdivides cn0 crab elimel simpl eleq1 elimdhyp simpr ax-1ne0
      1z neeq1 eqid divalglem10 dedth2h 3impb ) BEFZAEFZAGHZGCIZUAJZVNAKUBZLJZB
      DIZAMNZVNONZPZQZDERZCESZVKVLVMUCZWDVOVQVKBTUDZVTPZQZDERZCESVOVNWEATUDZKUB
      ZLJZWFVRWJMNZVNONZPZQZDERZCESBATTBWFPZWCWICEWRWBWHDEWRWAWGVOVQBWFVTUEUFUG
      UHAWJPZWIWQCEWSWHWPDEWSVQWLWGWOVOWSVPWKVNLAWJKUIUJWSVTWNWFWSVSWMVNOAWJVRM
      UKULUMUNUGUHWJWJWFVNUONUPJCUQURZWFCDBTEVEUSWEVLWJEFTEFATVLVMUTAWJEVATWJEV
      AVEVBWEVMWJGHTGHATVLVMVCAWJGVFTWJGVFVDVBWTVGVHVIVJ $.
      $( [21-Mar-2011] $)

    $( Express the division algorithm as stated in ~ divalg in terms of
       ` || ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgb $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) ->
                    ( E! r e. ZZ E. q e. ZZ ( 0 <_ r /\ r < ( abs ` D ) /\
                                              N = ( ( q x. D ) + r ) ) <->
                      E! r e. NN0 ( r < ( abs ` D ) /\ D || ( N - r ) ) ) ) $=
      ( cz wcel cc0 cv wbr co caddc wceq w3a wrex wreu wa cn0 wb cc zcn cle cfv
      cabs clt cmul cmin cdivides wne zsubcl divides sylan2 3impb 3com12 zmulcl
      syl subadd syl3an addcom syl2an 3adant1 eqeq1d bitrd eqcom 3bitr3g 3expia
      exp3acom23 3impia imp rexbidva 3com23 anbi2d df-3an rexbii bitri syl6rbbr
      wi r19.42v anass syl6bb 3expa reubidva elnn0z anbi1i eubii df-reu 3adant3
      weu 3bitr4ri ) BEFZAEFZGCHZUAIZWKAUCUBUDIZBDHZAUEJZWKKJZLZMZDENZCEOZWMABW
      KUFJZUGIZPZCQOZRAGUHWIWJPZWTWLXCPZCEOZXDXEWSXFCEWIWJWKEFZWSXFRWIWJXHMZWSW
      LWMPZXBPZXFXIXKXJWQDENZPZWSXIXBXLXJXIXBWOXALZDENZXLWJWIXHXBXORZWJWIXHXPWI
      XHPZWJXAEFXPBWKUIDAXAUJUKULUMWIXHWJXOXLRWIXHWJMZXNWQDEXRWNEFZXNWQRZWIXHWJ
      XSXTVPXQXSWJXTWIXHXSWJPZXTWIXHYAMZXAWOLZWPBLZXNWQYBYCWKWOKJZBLZYDWIBSFXHW
      KSFZYAWOSFZYCYFRBTWKTZYAWOEFYHWNAUNWOTUOZBWKWOUPUQYBYEWPBXHYAYEWPLZWIXHYG
      YHYKYAYIYJWKWOURUSUTVAVBXAWOVCWPBVCVDVEVFVGVHVIVJVBVKWSXJWQPZDENXMWRYLDEW
      LWMWQVLVMXJWQDEVQVNVOWLWMXBVRVSVTWAWKQFZXCPZCWGXHXFPZCWGXDXGYNYOCYNXHWLPZ
      XCPYOYMYPXCWKWBWCXHWLXCVRVNWDXCCQWEXFCEWEWHVSWF $.
      $( [31-Mar-2011] $)

    $( The division algorithm (theorem) for a positive divisor.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    divalg2 $p |- ( ( N e. ZZ /\ D e. NN ) ->
                    E! r e. NN0 ( r < D /\ D || ( N - r ) ) ) $=
      ( vq cz wcel cn wa cv cabs clt wbr co cn0 wreu cc0 w3a cle wceq mpbid cfv
      cmin cdivides wne nnz nnne0 jca cmul caddc divalg divalgb 3expb sylan2 wb
      wrex cr nnre nnnn0 nn0ge0 syl absid syl2anc breq2d anbi1d reubidv adantl
      ) BEFZAGFZHCIZAJUAZKLZABVIUBMUCLZHZCNOZVIAKLZVLHZCNOZVHVGAEFZAPUDZHVNVHVR
      VSAUEAUFUGVGVRVSVNVGVRVSQPVIRLVKBDIAUHMVIUIMSQDEUOCEOVNABCDUJABCDUKTULUMV
      HVNVQUNVGVHVMVPCNVHVKVOVLVHVJAVIKVHAUPFPARLZVJASAUQVHANFVTAURAUSUTAVAVBVC
      VDVEVFT $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D r z $.  $d N r z $.
    $( The result of the ` mod ` operator satisfies the requirements for the
       remainder ` r ` in the division algorithm for a positive divisor
       (compare ~ divalg2 and ~ divalgb ).  This demonstration theorem
       justifies the use of ` mod ` to yield an explicit remainder from this
       point forward.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgmod $p |- ( ( N e. ZZ /\ D e. NN ) -> ( r = ( N mod D ) <->
                      ( r e. NN0 /\ ( r < D /\ D || ( N - r ) ) ) ) ) $=
      ( vz cz wcel wa cv co clt wbr cmin cdivides cn0 wceq cr syl2an syl cc zcn
      cn cmo csn crab cuni wb zmodcl divalg2 breq1 oveq2 breq2d anbi12d reuuni2
      wreu syl2anc crp zre nnrp modlt cdiv cfl cfv cmul cc0 nnre redivcl syl3an
      wne 3anidm23 flcl nnz adantl nn0z zsubcl syldan nncn mulcom modval adantr
      nnne0 zmulcl anabss7 nn0cn subsub23 syl3anc eqcom 3bitr3g eqtr3d dvds0lem
      mpbid syl31anc mpbi2and eqcomd sneqd reuunisn eqtr4d eleq2d elsn elrab )
      BEFZAUAFZGZCHZBAUBIZUCZFXCDHZAJKZABXFLIZMKZGZDNUDZFXCXDOXCNFXCAJKZABXCLIZ
      MKZGZGXBXEXKXCXBXEXKUEZUCZXKXBXDXPXBXPXDXBXDAJKZABXDLIZMKZXPXDOZXBXDNFZXJ
      DNUNZXRXTGZYAUFBAUGZABDUHZXJYDDNXDXFXDOZXGXRXIXTXFXDAJUIYGXHXSAMXFXDBLUJU
      KULUMUOWTBPFZAUPFZXRXABUQZAURZBAUSQXBBAUTIZVAVBZEFZAEFZXSEFZYMAVCIZXSOXTX
      BYLPFZYNWTXAYRWTYHXAAPFXAAVDVHYRYJAVEAVTBAVFVGVIYLVJRZXAYOWTAVKZVLWTXAXDE
      FZYPXBYBUUAYEXDVMRBXDVNVOXBAYMVCIZYQXSXBASFZYMSFZUUBYQOXAUUCWTAVPVLXBYNUU
      DYSYMTRAYMVQUOXBXDBUUBLIZOZUUBXSOZWTYHYIUUFXAYJYKBAVRQXBUUEXDOZXSUUBOZUUF
      UUGXBBSFZUUBSFZXDSFZUUHUUIUFWTUUJXABTVSXBUUBEFZUUKWTXAUUMXAYOYNUUMXBYTYSA
      YMWAQWBUUBTRXBYBUULYEXDWCRBUUBXDWDWEUUEXDWFXSUUBWFWGWJWHYMAXSWIWKWLWMWNXB
      YCXKXQOYFXJDNWORWPWQCXDWRXJXODXCNXFXCOZXGXLXIXNXFXCAJUIUUNXHXMAMXFXCBLUJU
      KULWSWG $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d D r x $.  $d K r $.  $d N r x $.
    $( Corollary of the division algorithm.  If an integer ` D ` greater than
       ` 1 ` divides ` N ` , then it does not divide any of ` N - 1 ` ,
       ` N - 2 ` ... ` N - ( D - 1 ) ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    ndvdssub $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                     ( D || N -> -. D || ( N - K ) ) ) $=
      ( vr vx wcel clt wbr wa cdivides cmin co wi cn0 cc0 wceq wral breq1 oveq2
      breq2d cz cn wn w3a wne nnnn0 nnne0 jca df-ne anbi2i cv wrex wreu divalg2
      anbi12d reu4 sylib simprd nngt0 3ad2ant2 zcn subid1 biimpar 3adant2 3expa
      cc anim2i ancoms 0nn0 anbi2d eqeq2 imbi12d rcla4v ax-mp syl5 exp3a ralimi
      syl r19.21v pm2.43i 3impia eqeq1 syl5com pm4.14 syl6ib syl7bi exp4a com23
      imp4a syl7 imp3a 3expia ) CUAFZAUBFZBUBFZBAGHZIZACJHZACBKLZJHZUCZMWMWNIZW
      RWQXAWMWNWRWQXAMWMWNWRUDZWOWPXAXCWPWOXAWOBNFZBOUEZIXCWPXAWOXDXEBUFBUGUHXC
      WPXDXEXAXCXDWPXEXAMXCXDWPXEXAWPXEIWPBOPZUCZIZXCXDXAXEXGWPBOUIUJXCXDWPWTIZ
      XFMZXHXAMXCDUKZAGHZACXKKLZJHZIZXKOPZMZDNQZXDXJWMWNWRXRXBWRXRMXBXBWRXRXBXB
      WRIZXQMZDNQZXSXRMXBXOEUKZAGHZACYBKLZJHZIZIZXKYBPZMZENQZDNQZYAXBXODNULZYKX
      BXODNUMYLYKIACDUNXOYFDENYHXLYCXNYEXKYBAGRYHXMYDAJXKYBCKSTUOUPUQURYJXTDNYJ
      XSXOXPXSXOIXOOAGHZACOKLZJHZIZIZYJXPXOXSYQXSYPXOWMWNWRYPXCYMYOWNWMYMWRAUSU
      TWMWRYOWNWMYOWRWMYNCAJWMCVFFYNCPCVACVBVRTVCVDUHVEVGVHONFYJYQXPMZMVIYIYREO
      NYBOPZYGYQYHXPYSYFYPXOYSYCYMYEYOYBOAGRYSYDYNAJYBOCKSTUOVJYBOXKVKVLVMVNVOV
      PVQVRXSXQDNVSUQVPVTWAXQXJDBNXKBPZXOXIXPXFYTXLWPXNWTXKBAGRYTXMWSAJXKBCKSTU
      OXKBOWBVLVMWCWPWTXFWDWEWFWGWHWIWJWHWKWLWHWA $.
      $( [31-Mar-2011] $)
  $}

  $( Corollary of the division algorithm.  If an integer ` D ` greater than
     ` 1 ` divides ` N ` , then it does not divide any of ` N + 1 ` ,
     ` N + 2 ` ... ` N + ( D - 1 ) ` .  (Contributed by Paul Chapman,
     31-Mar-2011.) $)
  ndvdsadd $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                   ( D || N -> -. D || ( N + K ) ) ) $=
    ( cz wcel cn clt wbr wa w3a cdivides cmin co wn wi cc0 cr wb syl2an cc nnre
    caddc posdif syl2anr pm5.32i nnz zsubcl elnnz biimpri sylan anasss ltsubpos
    sylbi nngt0 biimpd expcom mpdi imp adantrr 3adant1 ndvdssub syld3an3 zaddcl
    jca sylan2 dvdssubr an12s wceq zcn nncn subsub3 syl3an breq2d bitr4d notbid
    3impb 3adant3r sylibrd ) CDEZAFEZBFEZBAGHZIZJACKHZACABLMZLMZKHZNZACBUBMZKHZ
    NZVSVTWCWEFEZWEAGHZIZWDWHOVTWCWNVSVTWCIWLWMVTWAWBWLVTWAIZWBIWOPWEGHZIWLWOWB
    WPWABQEZAQEZWBWPRVTBUAZAUAZBAUCUDUEWOWEDEZWPWLVTADEZBDEZXAWAAUFZBUFZABUGSWL
    XAWPIWEUHUIUJUMUKVTWAWMWBVTWAWMVTWAPBGHZWMBUNWAVTXFWMOWAVTIXFWMWAWQWRXFWMRV
    TWSWTBAULSUOUPUQURUSVDUTAWECVAVBVSVTWAWKWHRWBVSVTWAJZWJWGXGWJAWIALMZKHZWGVS
    VTWAWJXIRZVTVSWAXJVTXBWIDEZXJVSWAIXDWAVSXCXKXECBVCVEAWIVFSVGVPXGWFXHAKVSCTE
    VTATEWABTEWFXHVHCVIAVJBVJCABVKVLVMVNVOVQVR $.
    $( [31-Mar-2011] $)

  $( Special case of ~ ndvdsadd .  If an integer ` D ` greater than ` 1 `
     divides ` N ` , it does not divide ` N + 1 ` .  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  ndvdsp1 $p |- ( ( N e. ZZ /\ D e. NN /\ 1 < D ) ->
                  ( D || N -> -. D || ( N + 1 ) ) ) $=
    ( c1 clt wbr cz wcel cn wa cdivides caddc co wn 1nn jctl ndvdsadd syl3an3
    wi ) CADEZBFGAHGCHGZSIABJEABCKLJEMRSTNOACBPQ $.
    $( [31-Mar-2011] $)

  ${
    ndvdsi.1 $e |- A e. NN $.
    ndvdsi.2 $e |- Q e. NN0 $.
    ndvdsi.3 $e |- R e. NN $.
    ndvdsi.4 $e |- B = ( ( A x. Q ) + R ) $.
    ndvdsi.5 $e |- R < A $.
    $( A quick test for non-divisibility.  (Contributed by Mario Carneiro,
       18-Feb-2014.) $)
    ndvdsi $p |- -. A || B $=
      ( cdivides wbr cmul co caddc wn cz wcel nnzi mp2an cn dvdsmul1 clt zmulcl
      nn0zi wa wi pm3.2i ndvdsadd mp3an ax-mp breq2i mtbir ) ABJKAACLMZDNMZJKZA
      UMJKZUOOZAPQZCPQZUPAERZCFUDZACUASUMPQZATQDTQZDAUBKZUEUPUQUFURUSVBUTVAACUC
      SEVCVDGIUGADUMUHUIUJBUNAJHUKUL $.
      $( [18-Feb-2014] $)
  $}

