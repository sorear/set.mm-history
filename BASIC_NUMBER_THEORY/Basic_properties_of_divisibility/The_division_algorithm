$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           The division algorithm
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    divalglem0.1 $e |- N e. ZZ $.
    divalglem0.2 $e |- D e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem0 $p |- ( ( R e. ZZ /\ K e. ZZ ) -> ( D || ( N - R ) ->
                       D || ( N - ( R - ( K x. ( abs ` D ) ) ) ) ) ) $=
      ( cz wcel wa cmin co cdivides wbr cabs ax-mp wi mp3an1 syl2an cc zcn cmul
      cfv caddc iddvds wb dvdsabsb anidms mpbid cn0 nn0abscl dvdsmultr2 mp3an13
      nn0z mpi adantl zsubcl mpan zmulcl dvds2add mpan2d wceq syl subsub breq2d
      mpan2 sylibrd ) BGHZCGHZIZADBJKZLMZAVJCANUBZUAKZUCKZLMZADBVMJKJKZLMVIVKAV
      MLMZVOVHVQVGVHAVLLMZVQAGHZVRFVSAALMZVRAUDVSVTVRUEAAUFUGUHOVSVHVLGHZVRVQPF
      VLUIHZWAVSWBFAUJOVLUMOZACVLUKULUNUOVGVJGHZVMGHZVKVQIVOPZVHDGHZVGWDEDBUPUQ
      VHWAWEWCCVLURVEZVSWDWEWFFAVJVMUSQRUTVIVPVNALVGBSHZVMSHZVPVNVAZVHBTVHWEWJW
      HVMTVBDSHZWIWJWKWGWLEDTODBVMVCQRVDVF $.
      $( [21-Mar-2011] $)

    divalglem1.3 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem1 $p |- 0 <_ ( N + ( abs ` ( N x. D ) ) ) $=
      ( cmul co cabs cfv cle wbr cc0 zrei 0re wcel ax-mp cr wb recni abscli wne
      cneg caddc wo letrii c1 cn nnabscl mp2an nnge1 le0neg1 renegcli lemulge11
      cz wa mpanl12 sylanb mpan2 absmuli absnidi oveq1d syl5eq breqtrrd le0neg2
      remulcli absge0 letri sylbi jaoi cmin df-neg breq1i lesubadd2i bitri mpbi
      cc ) BUBZBAFGZHIZJKZLBVSUCGJKZBLJKZLBJKZUDVTBLBCMZNUEWBVTWCWBVQVQAHIZFGZV
      SJWBUFWEJKZVQWFJKZWEUGOZWGAUNOALUAWIDEAUHUIWEUJPWBLVQJKZWGWHBQOZWBWJRWDBU
      KPVQQOWEQOWJWGUOWHBWDULZAAADMZSZTVQWEUMUPUQURWBVSBHIZWEFGWFBABWDSWNUSWBWO
      VQWEFBWDUTVAVBVCWCVQLJKZVTWKWCWPRWDBVDPWPLVSJKZVTVRVPOWQVRBAWDWMVESZVRVFP
      VQLVSWLNVRWRTZVGURVHVIPVTLBVJGZVSJKWAVQWTVSJBVKVLLBVSNWDWSVMVNVO $.
      $( [21-Mar-2011] $)

    $d D r $.  $d N r $.
    divalglem2.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem2 $p |- sup ( S , RR , `' < ) e. S $=
      ( wcel cmul co cabs cfv cn0 cc0 wbr cz ax-mp cmin cdivides c0 wne cr ccnv
      clt csup caddc cle divalglem1 elnn0z zmulcl mp2an nn0abscl zaddcl mpbiran
      nn0z mpbir cv wceq oveq2 breq2d elrab2 iddvds wb dvdsabsb anidms mpbid wi
      cneg znegcl dvdsmultr2 cc zcn absmuli negeqi df-neg subidi oveq1i subsub4
      mp3an 3eqtr2ri recni mulneg1i 3eqtr4i breqtrri mpbiran2 ne0i cuz wss crab
      abscli ssrab2 eqsstri nn0uz sseqtri infmssuzcl mpan ) BUAUBZBUCUEUDUFBIZC
      CAJKZLMZUGKZBIZWRXCXBNIZXDOXBUHPZACEFGUIXDXBQIZXEXBUJCQIZXAQIZXFEXANIZXHW
      TQIZXIXGAQIZXJEFCAUKULWTUMRXAUPRZCXAUNULUOUQXCXDACXBSKZTPZACDURZSKZTPZXND
      XBNBXOXBUSXPXMATXOXBCSUTVAHVBACLMZVIZALMZJKZXMTAXTTPZAYATPZXKYBFXKAATPZYB
      AVCXKYDYBVDAAVEVFVGRXKXSQIZXTQIZYBYCVHFXRQIZYEXRNIZYGXGYHECUMRXRUPRXRVJRX
      TNIZYFXKYIFAUMRXTUPRAXSXTVKVTRXAVIZXRXTJKZVIXMYAXAYKCAXGCVLIZECVMRZXKAVLI
      FAVMRZVNVOYJOXASKCCSKZXASKZXMXAVPYOOXASCYMVQVRYLYLXAVLIZYPXMUSYMYMXHYQXLX
      AVMRCCXAVSVTWAXRXTXRCYMWKWBXTAYNWKWBWCWDWEWFUQBXBWGRBOWHMZWIWRWSBNYRBXQDN
      WJNHXQDNWLWMWNWOBOWPWQR $.
      $( [21-Mar-2011] $)

    $d D q r x z $.  $d N q r x z $.  $d R x $.  $d S z $.
    $( Lemma for ~ divalg . $)
    divalglem4 $p |- S = { r e. NN0 | E. q e. ZZ N = ( ( q x. D ) + r ) } $=
      ( cv co caddc wceq cz wrex cn0 wcel cmin cdivides wa vz cmul crab wb nn0z
      wbr zsubcl mpan divides 3syl eqcom cc nn0cn zmulcl mpan2 zcn ax-mp subadd
      syl mp3an1 addcom eqeq1d bitrd syl2an syl6bb syl5bbr pm5.32i oveq2 breq2d
      rexbidva elrab2 eqeq2d rexbidv elrab 3bitr4i eqriv ) UABCEJZAUBKZDJZLKZMZ
      ENOZDPUCZUAJZPQZACWDRKZSUFZTWECVRWDLKZMZENOZTWDBQWDWCQWEWGWJWEWGVRWFMZENO
      ZWJWEWDNQZWFNQZWGWLUDZWDUECNQZWMWNFCWDUGUHANQZWNWOGEAWFUIUHUJWEWKWIENWKWF
      VRMZWEVQNQZTZWIWFVRUKWTWRWHCMZWIWEWDULQZVRULQZWRXAUDWSWDUMWSVRNQZXCWSWQXD
      GVQAUNUOVRUPUSXBXCTZWRWDVRLKZCMZXACULQZXBXCWRXGUDWPXHFCUPUQCWDVRURUTXEXFW
      HCWDVRVAVBVCVDWHCUKVEVFVJVCVGACVSRKZSUFWGDWDPBVSWDMZXIWFASVSWDCRVHVIIVKWB
      WJDWDPXJWAWIENXJVTWHCVSWDVRLVHVLVMVNVOVP $.
      $( [21-Mar-2011] $)

    divalglem5.5 $e |- R = sup ( S , RR , `' < ) $.
    $( Lemma for ~ divalg . $)
    divalglem5 $p |- ( 0 <_ R /\ R < ( abs ` D ) ) $=
      ( vx cc0 cle wbr cn0 wcel cmin co cdivides ax-mp cabs cfv wa cr ccnv csup
      clt divalglem2 eqeltri wceq oveq2 breq2d crab cbvrabv eqtri elrab2 simpli
      cv nn0ge0 wn cn cz wne nnabscl mp2an nngt0 wb 0re cc zcn abscli ltnle cuz
      mpbi wss ssrab2 eqsstri nn0uz sseqtri nn0abscl nn0sub2 mp3an1 mpan a1i wi
      nn0z c1 cmul divalglem0 mpan2 recni mulid2i oveq2i breq2i syl6ib sylanbrc
      syl imp infmssuzle sylancr syl5eqbr simpld nn0re lesub mp3an3 recnd subid
      1z syl2anc bitrd mpbid mto mpbir pm3.2i ) LBMNZBAUAUBZUGNZBOPZXOXRADBQRZS
      NZBCPXRXTUCZBCUDUGUEUFZCJACDEFGHIUHUIADKURZQRZSNZXTKBOCYCBUJYDXSASYCBDQUK
      ULCADEURZQRZSNZEOUMZYEKOUMIYHYEEKOYFYCUJYGYDASYFYCDQUKULUNUOZUPVNZUQZBUST
      XQXPBMNZUTZYMXPLMNZLXPUGNZYOUTZXPVAPZYPAVBPZALVCYRGHAVDVEXPVFTLUDPXPUDPZY
      PYQVGVHAYSAVIPGAVJTVKZLXPVLVEVNYMBBXPQRZMNZYOYMBYBUUBMJYMCLVMUBZVOUUBCPZY
      BUUBMNCOUUDCYIOIYHEOVPVQVRVSYMUUBOPZADUUBQRZSNZUUEXRYMUUFYLXPOPZXRYMUUFYS
      UUIGAVTTXPBWAWBWCYMYAUUHYAYMYKWDZXRXTUUHXRBVBPZXTUUHWEBWFUUKXTADBWGXPWHRZ
      QRZQRZSNZUUHUUKWGVBPXTUUOWEXHABWGDFGWIWJUUNUUGASUUMUUBDQUULXPBQXPXPUUAWKW
      LWMWMWNWOWQWRWQYEUUHKUUBOCYCUUBUJYDUUGASYCUUBDQUKULYJUPWPUUBCLWSWTXAYMUUC
      XPBBQRZMNZYOYMBUDPZUURUUCUUQVGZYMXRUURYMXRXTUUJXBBXCZWQZUVAUURUURYTUUSUUA
      BBXPXDXEXIYMUUPLXPMYMBVIPUUPLUJYMBUVAXFBXGWQULXJXKXLUURXQYNVGZXRUURYLUUTT
      UURYTUVBUUABXPVLWJTXMXN $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem6.1 $e |- A e. NN $.
    divalglem6.2 $e |- X e. ( 0 ... ( A - 1 ) ) $.
    divalglem6.3 $e |- K e. ZZ $.
    $( Lemma for ~ divalg . $)
    divalglem6 $p |- ( K =/= 0 ->
                       -. ( X + ( K x. A ) ) e. ( 0 ... ( A - 1 ) ) ) $=
      ( cc0 clt wbr co caddc c1 cmin wcel cle cr wb cz 0z mp2an wne wo cmul cfz
      wn zrei 0re lttri2i cneg lt0neg1 ax-mp znegcl zltp1le ax-1cn breq1i bitri
      addid2i renegcli wa nnrei nn0ge0i lemulge12 an4s mpanl12 mpan sylbi recni
      nnnn0i mulneg1i oveq2i remulcli subnegi eqtri suble0 bitr3i sylibr w3a cn
      nnz elfzm11 mpbi simp3i simp1i ltadd1i readdcli ltletri syl ltnle simp2bi
      sylib nsyl simp2i addge02 letri mpan2 lenlt simp3bi jaoi ) BGUABGHIZGBHIZ
      UBCBAUCJZKJZGALMJUDJZNZUEZBGBFUFZUGUHWSXEWTWSGXBOIZXDWSXBGHIZXGUEZWSAXAKJ
      ZGOIZXHWSABUIZAUCJZOIZXKWSLXLOIZXNWSGXLHIZXOBPNZWSXPQXFBUJUKXPGLKJZXLOIZX
      OGRNZXLRNZXPXSQSBRNZYAFBULUKGXLUMTXRLXLOLUNUQZUOUPUPXLPNZXOXNBXFURZAPNZGA
      OIZYDXOUSXNADUTZAADVHVAZYFYDYGXOXNAXLVBVCVDVEVFXKAXMMJZGOIZXNYJXJGOYJAXAU
      IZMJXJXMYLAMBABXFVGAYHVGZVIVJAXAYMXABAXFYHVKZVGVLVMUOYFXMPNYKXNQYHXLAYEYH
      VKAXMVNTVOVPXBXJHIZXKXHCAHIZYOCRNZGCOIZYPCXCNZYQYRYPVQZEXTARNZYSYTQSAVRNU
      UADAVSUKZCGAVTTWAZWBCAXACYQYRYPUUCWCUFZYHYNWDWAXBXJGCXAUUDYNWEZAXAYHYNWEU
      GWFVEWGXBPNZGPNXHXIQUUEUGXBGWHTWJXDXBRNZXGXBAHIZXTUUAXDUUGXGUUHVQQSUUBXBG
      AVTTZWIWKWTUUHXDWTAXBOIZUUHUEZWTAXAOIZUUJWTLBOIZUULWTXRBOIZUUMXTYBWTUUNQS
      FGBUMTXRLBOYCUOUPYGUUMUULYIYFXQYGUUMUSUULYHXFABVBVDVEVFUULXAXBOIZUUJYRUUO
      YQYRYPUUCWLXAPNCPNYRUUOQYNUUDXACWMTWAAXAXBYHYNUUEWNWOWGYFUUFUUJUUKQYHUUEA
      XBWPTWJXDUUGXGUUHUUIWQWKWRVF $.
      $( [21-Mar-2011] $)
  $}

  ${
    divalglem7.1 $e |- D e. ZZ $.
    divalglem7.2 $e |- D =/= 0 $.
    $( Lemma for ~ divalg . $)
    divalglem7 $p |- ( ( X e. ( 0 ... ( ( abs ` D ) - 1 ) ) /\ K e. ZZ ) ->
                       ( K =/= 0 -> -. ( X + ( K x. ( abs ` D ) ) ) e.
                                       ( 0 ... ( ( abs ` D ) - 1 ) ) ) ) $=
      ( cc0 co wcel cz wne cmul caddc wn wi cif wceq oveq1 eleq1d notbid 0z cfv
      cabs c1 cmin cfz imbi2d neeq1 oveq2d imbi12d wa cn pm3.2i nnabscl cle wbr
      ax-mp clt 0re leidi nngt0i w3a nnz elfzm11 mp2an biimpri mp3an divalglem6
      wb elimel dedth2h ) CFAUBUAZUCUDGUEGZHZBIHZBFJZCBVKKGZLGZVLHZMZNVOVMCFOZV
      PLGZVLHZMZNVNBFOZFJZVTWDVKKGZLGZVLHZMZNCBFFCVTPZVSWCVOWJVRWBWJVQWAVLCVTVP
      LQRSUFBWDPZVOWEWCWIBWDFUGWKWBWHWKWAWGVLWKVPWFVTLBWDVKKQUHRSUIVKWDVTAIHZAF
      JZUJVKUKHZWLWMDEULAUMUPZCFVLFIHZFFUNUOZFVKUQUOZFVLHZTFURUSVKWOUTWSWPWQWRV
      AZWPVKIHZWSWTVHTWNXAWOVKVBUPFFVKVCVDVEVFVIBFITVIVGVJ $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r x z $.  $d N q r x z $.  $d S x z $.  $d X z $.  $d Y z $.
    divalglem8.1 $e |- N e. ZZ $.
    divalglem8.2 $e |- D e. ZZ $.
    divalglem8.3 $e |- D =/= 0 $.
    divalglem8.4 $e |- S = { r e. NN0 | D || ( N - r ) } $.
    $( Lemma for ~ divalg . $)
    divalglem8 $p |-
        ( ( ( X e. S /\ Y e. S ) /\ ( X < ( abs ` D ) /\ Y < ( abs ` D ) ) ) ->
          ( K e. ZZ -> ( ( K x. ( abs ` D ) ) = ( Y - X ) -> X = Y ) ) ) $=
      ( wcel cz co wceq wi wa cc0 cc cn0 vz cabs cfv clt wbr cmul cmin caddc wb
      w3a cv cdivides crab ssrab2 eqsstri nn0sscn sstri sseli wne nnabscl mp2an
      cn nnz ax-mp zmulcl mpan2 zcn subadd syl3an 3com12 eqcom bibi12i 3adant1r
      syl sylib 3adant2r wn c1 cfz breq1 eleq1 imbi12d cle elnn0z anim1i df-3an
      sylibr 0z elfzm11 ex rgen vtoclri biimpd sylan9 com23 3ad2ant2 divalglem7
      sylan 3adant2 con2d syld df-ne con2bii syl6ibr sylbid oveq1 mul02i syl6eq
      eqeq1d biimpac subeq0 syl2an ancoms syl5ib adantr an4s 3adant3 exp3a mpdd
      imp 3expia ) EBLZEAUBUCZUDUEZFBLZFYCUDUEZCMLZCYCUFNZFEUGNZOZEFOZPZPYBYDQZ
      YEYFQZYGYLYMYNYGUJZYJCROZYKYOYJFEYHUHNZOZYPYMYEYGYJYRUIZYFYBYEYGYSYDYBYEY
      GUJYIYHOZYQFOZUIZYSYEYBYGUUBYEFSLZYBESLZYGYHSLZUUBBSFBTSBADGUKUGNULUEZGTU
      MTKUUFGTUNUOZUPUQZURZBSEUUHURZYGYHMLZUUEYGYCMLZUUKYCVBLZUULAMLARUSUUMIJAU
      TVAYCVCVDZCYCVEVFYHVGVNFEYHVHVIVJYTYJUUAYRYIYHVKYQFVKVLVOVMVPYOYRCRUSZVQZ
      YPYOYRYQRYCVRUGNVSNZLZUUPYNYMYRUURPZYGYEYFUUSYEYRYFUURYEYRYFUURPYEYFFUUQL
      ZYRUURUAUKZYCUDUEZUVAUUQLZPZYFUUTPUAFBUVAFOUVBYFUVCUUTUVAFYCUDVTUVAFUUQWA
      WBUVDUABUVABLZUVBUVCUVEUVBQZUVAMLZRUVAWCUEZUVBUJZUVCUVFUVGUVHQZUVBQUVIUVE
      UVJUVBUVEUVATLUVJBTUVAUUGURUVAWDVOWEUVGUVHUVBWFWGRMLUULUVCUVIUIWHUUNUVARY
      CWIVAWGWJWKZWLYRUUTUURFYQUUQWAWMWNWJWOXTWPYOUUOUURYMYGUUOUURVQPZYNYMEUUQL
      ZYGUVLYBYDUVMUVDYDUVMPUAEBUVAEOUVBYDUVCUVMUVAEYCUDVTUVAEUUQWAWBUVKWLXTACE
      IJWQWRWSWTXAUUOYPCRXBXCXDXEYOYJYPYKYMYNYJYPQZYKPZYGYBYEYDYFUVOYBYEQZUVOYD
      YFQUVNRYIOZUVPYKYPYJUVQYPYHRYIYPYHRYCUFNRCRYCUFXFYCUULYCSLUUNYCVGVDXGXHXI
      XJUVPYIROZFEOZUIZUVQYKUIYEYBUVTYEUUCUUDUVTYBUUIUUJFEXKXLXMUVRUVQUVSYKYIRV
      KFEVKVLVOXNXOXPXQXRXSYAXP $.
      $( [21-Mar-2011] $)

    ${
      $d D k r x y $.  $d R x $.  $d S k x y $.
      divalglem9.5 $e |- R = sup ( S , RR , `' < ) $.
      $( Lemma for ~ divalg . $)
      divalglem9 $p |- E! x e. S x < ( abs ` D ) $=
        ( wbr wa wceq wcel cc0 co cmin cz cdivides vy vk cabs cfv clt wreu wrex
        cv wi wral breq1 reu4 cr ccnv csup divalglem2 eqeltri divalglem5 simpri
        cle rcla4ev mp2an cmul oveq2 breq2d elrab2 simprbi anim12i simplbi nn0z
        cn0 syl zsubcl mpan syl2an dvds2sub mp3an1 mpd cc zcn caddc recni subid
        zrei ax-mp oveq1i 0cn subsub2 syl5eq sub4 mpanl12 ancoms addid2 3eqtr3d
        subcl mpbid wb absdvdsb sylancr wne nnabscl nnz divides adantr rexlimdv
        cn divalglem8 ex rgen2 mpbir2an ) AUHZBUCUDZUELZADUFXMADUGZXMUAUHZXLUEL
        ZMZXKXONZUIZUADUJADUJXMXPAUADXKXOXLUEUKULCDOCXLUELZXNCDUMUEUNUODKBDEFGH
        IJUPUQPCUTLXTBCDEFGHIJKURUSXMXTACDXKCXLUEUKVAVBXSAUADDXKDOZXODOZMZXQXRY
        CXQMZUBUHZXLVCQXOXKRQZNZUBSUGZXRYCYHXQYCXLYFTLZYHYCBYFTLZYIYCBEXKRQZEXO
        RQZRQZTLZYJYCBYKTLZBYLTLZMZYNYAYOYBYPYAXKVKOZYOBEFUHZRQZTLZYOFXKVKDYSXK
        NYTYKBTYSXKERVDVEJVFZVGYBXOVKOZYPUUAYPFXOVKDYSXONYTYLBTYSXOERVDVEJVFZVG
        VHYCYKSOZYLSOZMZYQYNUIZYAXKSOZXOSOZUUGYBYAYRUUIYAYRYOUUBVIXKVJVLZYBUUCU
        UJYBUUCYPUUDVIXOVJVLZUUIUUEUUJUUFESOZUUIUUEGEXKVMVNUUMUUJUUFGEXOVMVNVHV
        OBSOZUUEUUFUUHHBYKYLVPVQVLVRYCYMYFBTYAUUIUUJYMYFNZYBUUKUULUUIXKVSOZXOVS
        OZUUOUUJXKVTXOVTUUPUUQMZEERQZXKXORQZRQZPYFWAQZYMYFUURUVAPUUTRQZUVBUUSPU
        UTREVSOZUUSPNEEGWDWBZEWCWEWFPVSOUUPUUQUVCUVBNWGPXKXOWHVQWIUVDUVDUURUVAY
        MNUVEUVEEEXKXOWJWKUURYFVSOZUVBYFNUUQUUPUVFXOXKWOWLYFWMVLWNVOVOVEWPYAUUI
        UUJYJYIWQZYBUUKUULUUIUUJMZUUNYFSOZUVGHUUJUUIUVIXOXKVMWLZBYFWRWSVOWPYAUU
        IUUJYIYHWQZYBUUKUULUVHXLSOZUVIUVKXLXFOZUVLUUNBPWTUVMHIBXAVBXLXBWEUVJUBX
        LYFXCWSVOWPXDYDYGXRUBSBDYEEXKXOFGHIJXGXEVRXHXIXJ $.
        $( [21-Mar-2011] $)
    $}

    $( Lemma for ~ divalg . $)
    divalglem10 $p |- E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) $=
      ( vx cc0 cv wbr clt caddc wceq cz wrex wa anbi2i cle cabs cfv cmul co w3a
      wreu cr ccnv csup eqid divalglem9 weu cn0 oveq2 eqeq2d rexbidv divalglem4
      wcel elrab2 ancom 3bitr4i elnn0z an12 3bitri anbi1i df-3an rexbii r19.42v
      anass bitri bitr4i eubii df-reu mpbi breq2 breq1 3anbi123d cbvreuv ) KJLZ
      UAMZVTAUBUCZNMZCELAUDUEZVTOUEZPZUFZEQRZJQUGZKDLZUAMZWJWBNMZCWDWJOUEZPZUFZ
      EQRZDQUGWCJBUGZWIJABUHNUIUJZBCDFGHIWRUKULVTBUSZWCSZJUMVTQUSZWHSZJUMWQWIWT
      XBJWTXAWAWCSZWFEQRZSZSZXBWTWCVTUNUSZSZXDSZXAXCSZXDSXFWCWSSWCXGXDSZSWTXIWS
      XKWCWNEQRXDDVTUNBWJVTPZWNWFEQXLWMWECWJVTWDOUOUPUQABCDEFGHIURUTTWSWCVAWCXG
      XDVJVBXHXJXDXHWCXAWASZSXAWCWASZSXJXGXMWCVTVCTWCXAWAVDXNXCXAWCWAVATVEVFXAX
      CXDVJVEWHXEXAWHXCWFSZEQRXEWGXOEQWAWCWFVGVHXCWFEQVIVKTVLVMWCJBVNWHJQVNVBVO
      WHWPJDQVTWJPZWGWOEQXPWAWKWCWLWFWNVTWJKUAVPVTWJWBNVQXPWEWMCVTWJWDOUOUPVRUQ
      VSVO $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D q r $.  $d N q r $.
    $( The division algorithm (theorem).  Dividing an integer ` N ` by a
       nonzero integer ` D ` produces a (unique) quotient ` q ` and a unique
       remainder ` 0 <_ r < ( abs `` D ) ` .  The proof does not use ` / ` ,
       ` |_ ` or ` mod ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divalg $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) -> E! r e. ZZ E. q e. ZZ
                   ( 0 <_ r /\ r < ( abs ` D ) /\ N = ( ( q x. D ) + r ) ) ) $=
      ( cz wcel cc0 wne cv wbr cabs clt cmul co caddc wceq w3a wrex wreu c1 cle
      cfv wa cif eqeq1 3anbi3d rexbidv reubidv fveq2 breq2d oveq2 oveq1d eqeq2d
      3anbi23d cmin cdivides cn0 crab elimel simpl eleq1 elimdhyp simpr ax-1ne0
      1z neeq1 eqid divalglem10 dedth2h 3impb ) BEFZAEFZAGHZGCIZUAJZVNAKUBZLJZB
      DIZAMNZVNONZPZQZDERZCESZVKVLVMUCZWDVOVQVKBTUDZVTPZQZDERZCESVOVNWEATUDZKUB
      ZLJZWFVRWJMNZVNONZPZQZDERZCESBATTBWFPZWCWICEWRWBWHDEWRWAWGVOVQBWFVTUEUFUG
      UHAWJPZWIWQCEWSWHWPDEWSVQWLWGWOVOWSVPWKVNLAWJKUIUJWSVTWNWFWSVSWMVNOAWJVRM
      UKULUMUNUGUHWJWJWFVNUONUPJCUQURZWFCDBTEVEUSWEVLWJEFTEFATVLVMUTAWJEVATWJEV
      AVEVBWEVMWJGHTGHATVLVMVCAWJGVFTWJGVFVDVBWTVGVHVIVJ $.
      $( [21-Mar-2011] $)

    $( Express the division algorithm as stated in ~ divalg in terms of
       ` || ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgb $p |- ( ( N e. ZZ /\ D e. ZZ /\ D =/= 0 ) ->
                    ( E! r e. ZZ E. q e. ZZ ( 0 <_ r /\ r < ( abs ` D ) /\
                                              N = ( ( q x. D ) + r ) ) <->
                      E! r e. NN0 ( r < ( abs ` D ) /\ D || ( N - r ) ) ) ) $=
      ( cz wcel cc0 cv wbr co caddc wceq w3a wrex wreu wa cn0 wb cc zcn cle cfv
      cabs clt cmul cmin cdivides wne zsubcl divides sylan2 3impb 3com12 zmulcl
      ax-17 wi syl subadd syl3an addcom syl2an 3adant1 eqeq1d bitrd eqcom sylib
      bibi12i 3expia exp3a com23 3impia imp rexbida 3com23 anbi2d df-3an rexbii
      bitri syl6rbbr anass syl6bb 3expa reubidva weu elnn0z anbi1i eubii df-reu
      r19.42v 3bitr4ri 3adant3 ) BEFZAEFZGCHZUAIZWNAUCUBUDIZBDHZAUEJZWNKJZLZMZD
      ENZCEOZWPABWNUFJZUGIZPZCQOZRAGUHWLWMPZXCWOXFPZCEOZXGXHXBXICEWLWMWNEFZXBXI
      RWLWMXKMZXBWOWPPZXEPZXIXLXNXMWTDENZPZXBXLXEXOXMXLXEWRXDLZDENZXOWMWLXKXEXR
      RZWMWLXKXSWLXKPZWMXDEFXSBWNUIDAXDUJUKULUMWLXKWMXRXORWLXKWMMZXQWTDEYADUOYA
      WQEFZXQWTRZWLXKWMYBYCUPXTYBWMYCXTYBWMYCWLXKYBWMPZYCWLXKYDMZXDWRLZWSBLZRYC
      YEYFWNWRKJZBLZYGWLBSFXKWNSFZYDWRSFZYFYIRBTWNTZYDWREFYKWQAUNWRTUQZBWNWRURU
      SYEYHWSBXKYDYHWSLZWLXKYJYKYNYDYLYMWNWRUTVAVBVCVDYFXQYGWTXDWRVEWSBVEVGVFVH
      VIVJVKVLVMVNVDVOXBXMWTPZDENXPXAYODEWOWPWTVPVQXMWTDEWIVRVSWOWPXEVTWAWBWCWN
      QFZXFPZCWDXKXIPZCWDXGXJYQYRCYQXKWOPZXFPYRYPYSXFWNWEWFXKWOXFVTVRWGXFCQWHXI
      CEWHWJWAWK $.
      $( [31-Mar-2011] $)

    $( The division algorithm (theorem) for a positive divisor.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    divalg2 $p |- ( ( N e. ZZ /\ D e. NN ) ->
                    E! r e. NN0 ( r < D /\ D || ( N - r ) ) ) $=
      ( vq cz wcel cn wa cv cabs clt wbr co cn0 wreu cc0 w3a cle wceq mpbid cfv
      cmin cdivides wne nnz nnne0 jca cmul caddc divalg divalgb 3expb sylan2 wb
      wrex cr nnre nnnn0 nn0ge0 syl absid syl2anc breq2d anbi1d reubidv adantl
      ) BEFZAGFZHCIZAJUAZKLZABVIUBMUCLZHZCNOZVIAKLZVLHZCNOZVHVGAEFZAPUDZHVNVHVR
      VSAUEAUFUGVGVRVSVNVGVRVSQPVIRLVKBDIAUHMVIUIMSQDEUOCEOVNABCDUJABCDUKTULUMV
      HVNVQUNVGVHVMVPCNVHVKVOVLVHVJAVIKVHAUPFPARLZVJASAUQVHANFVTAURAUSUTAVAVBVC
      VDVEVFT $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d D r z $.  $d N r z $.
    $( The result of the ` mod ` operator satisfies the requirements for the
       remainder ` r ` in the division algorithm for a positive divisor
       (compare ~ divalg2 and ~ divalgb ).  This demonstration theorem
       justifies the use of ` mod ` to yield an explicit remainder from this
       point forward.  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    divalgmod $p |- ( ( N e. ZZ /\ D e. NN ) -> ( r = ( N mod D ) <->
                      ( r e. NN0 /\ ( r < D /\ D || ( N - r ) ) ) ) ) $=
      ( vz co wceq wcel cz wa cn0 clt wbr cmin cdivides wb syl2anc cr syl2an cc
      syl cv cmo csn cn elsn crab cuni wreu zmodcl divalg2 breq1 breq2d anbi12d
      oveq2 reuuni2 crp zre nnrp modlt cdiv cfl cfv cmul cc0 nnre nnne0 redivcl
      wne syl3an 3anidm23 flcl nnz adantl simpl nn0z zsubcl recnd mulcom modval
      zcn adantr zmulcl an12s anabsan2 nn0cn subsub23 syl3anc eqcom sylib mpbid
      eqtr3d dvds0lem syl31anc mpbi2and eqcomd sneqd eqtr4d eleq2d elrab syl6bb
      bibi12i reuunisn syl5bbr ) CUAZBAUBEZFXDXEUCZGZBHGZAUDGZIZXDJGXDAKLZABXDM
      EZNLZIZIZCXEUEXJXGXDDUAZAKLZABXPMEZNLZIZDJUFZGXOXJXFYAXDXJXFYAUGZUCZYAXJX
      EYBXJYBXEXJXEAKLZABXEMEZNLZYBXEFZXJXEJGZXTDJUHZYDYFIZYGOBAUIZABDUJZXTYJDJ
      XEXPXEFZXQYDXSYFXPXEAKUKYMXRYEANXPXEBMUNULUMUOPXHBQGZAUPGZYDXIBUQZAURZBAU
      SRXJBAUTEZVAVBZHGZAHGZYEHGZYSAVCEZYEFYFXJYRQGZYTXHXIUUDXHYNXIAQGXIAVDVHUU
      DYPAVEZAVFBAVGVIVJYRVKTZXIUUAXHAVLZVMXJXHXEHGZUUBXHXIVNXJYHUUHYKXEVOTBXEV
      PPXJAYSVCEZUUCYEXJASGZYSSGZUUIUUCFXIUUJXHXIAUUEVQVMXJYTUUKUUFYSVTTAYSVRPX
      JXEBUUIMEZFZUUIYEFZXHYNYOUUMXIYPYQBAVSRXJUULXEFZYEUUIFZOZUUMUUNOXJBSGZUUI
      SGZXESGZUUQXHUURXIXHBYPVQWAXJUUIHGZUUSXHXIUVAXIXHXIUVAXIUUAYTUVAXJUUGUUFA
      YSWBRWCWDUUIVTTXJYHUUTYKXEWETBUUIXEWFWGUUOUUMUUPUUNUULXEWHYEUUIWHXAWIWJWK
      YSAYEWLWMWNWOWPXJYIYAYCFYLXTDJXBTWQWRXTXNDXDJXPXDFZXQXKXSXMXPXDAKUKUVBXRX
      LANXPXDBMUNULUMWSWTXC $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d D r x $.  $d K r $.  $d N r x $.
    $( Corollary of the division algorithm.  If an integer ` D ` greater than
       ` 1 ` divides ` N ` , then it does not divide any of ` N - 1 ` ,
       ` N - 2 ` ... ` N - ( D - 1 ) ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    ndvdssub $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                     ( D || N -> -. D || ( N - K ) ) ) $=
      ( vr vx wcel clt wbr wa cdivides cmin co wi cn0 cc0 wceq wral breq1 oveq2
      breq2d cz cn wn w3a wne nnnn0 nnne0 jca df-ne anbi2i cv wrex wreu divalg2
      anbi12d reu4 sylib simprd nngt0 3ad2ant2 zcn subid1 biimpar 3adant2 3expa
      cc anim2i ancoms 0nn0 anbi2d eqeq2 imbi12d rcla4v ax-mp syl5 exp3a ralimi
      syl r19.21v pm2.43i 3impia eqeq1 syl5com pm4.14 syl6ib syl7bi exp4a com23
      imp4a syl7 imp3a 3expia ) CUAFZAUBFZBUBFZBAGHZIZACJHZACBKLZJHZUCZMWMWNIZW
      RWQXAWMWNWRWQXAMWMWNWRUDZWOWPXAXCWPWOXAWOBNFZBOUEZIXCWPXAWOXDXEBUFBUGUHXC
      WPXDXEXAXCXDWPXEXAMXCXDWPXEXAWPXEIWPBOPZUCZIZXCXDXAXEXGWPBOUIUJXCXDWPWTIZ
      XFMZXHXAMXCDUKZAGHZACXKKLZJHZIZXKOPZMZDNQZXDXJWMWNWRXRXBWRXRMXBXBWRXRXBXB
      WRIZXQMZDNQZXSXRMXBXOEUKZAGHZACYBKLZJHZIZIZXKYBPZMZENQZDNQZYAXBXODNULZYKX
      BXODNUMYLYKIACDUNXOYFDENYHXLYCXNYEXKYBAGRYHXMYDAJXKYBCKSTUOUPUQURYJXTDNYJ
      XSXOXPXSXOIXOOAGHZACOKLZJHZIZIZYJXPXOXSYQXSYPXOWMWNWRYPXCYMYOWNWMYMWRAUSU
      TWMWRYOWNWMYOWRWMYNCAJWMCVFFYNCPCVACVBVRTVCVDUHVEVGVHONFYJYQXPMZMVIYIYREO
      NYBOPZYGYQYHXPYSYFYPXOYSYCYMYEYOYBOAGRYSYDYNAJYBOCKSTUOVJYBOXKVKVLVMVNVOV
      PVQVRXSXQDNVSUQVPVTWAXQXJDBNXKBPZXOXIXPXFYTXLWPXNWTXKBAGRYTXMWSAJXKBCKSTU
      OXKBOWBVLVMWCWPWTXFWDWEWFWGWHWIWJWHWKWLWHWA $.
      $( [31-Mar-2011] $)
  $}

  $( Corollary of the division algorithm.  If an integer ` D ` greater than
     ` 1 ` divides ` N ` , then it does not divide any of ` N + 1 ` ,
     ` N + 2 ` ... ` N + ( D - 1 ) ` .  (Contributed by Paul Chapman,
     31-Mar-2011.) $)
  ndvdsadd $p |- ( ( N e. ZZ /\ D e. NN /\ ( K e. NN /\ K < D ) ) ->
                   ( D || N -> -. D || ( N + K ) ) ) $=
    ( cz wcel cn clt wbr wa w3a cdivides cmin co wn wi cc0 wb cr syl2an cc nnre
    caddc posdif ancoms pm5.32i nnz zsubcl elnnz sylan sylbi anasss nngt0 ancli
    biimpri ltsubpos biimpd expcom imp3a syl5 imp adantrr jca ndvdssub syld3an3
    3adant1 zaddcl sylan2 dvdssubr an12s 3impb zcn subsub3 syl3an breq2d bitr4d
    wceq nncn notbid 3adant3r sylibrd ) CDEZAFEZBFEZBAGHZIZJACKHZACABLMZLMZKHZN
    ZACBUBMZKHZNZWAWBWEWGFEZWGAGHZIZWFWJOWBWEWPWAWBWEIWNWOWBWCWDWNWBWCIZWDIWQPW
    GGHZIWNWQWDWRWCWBWDWRQZWCBREZAREZWSWBBUAZAUAZBAUCSUDUEWQWGDEZWRWNWBADEZBDEZ
    XDWCAUFZBUFZABUGSWNXDWRIWGUHUNUIUJUKWBWCWOWDWBWCWOWCWCPBGHZIWBWOWCXIBULUMWB
    WCXIWOWCWBXIWOOWCWBIXIWOWCWTXAXIWOQWBXBXCBAUOSUPUQURUSUTVAVBVEAWGCVCVDWAWBW
    CWMWJQWDWAWBWCJZWLWIXJWLAWKALMZKHZWIWAWBWCWLXLQZWBWAWCXMWBXEWKDEZXMWAWCIXGW
    CWAXFXNXHCBVFVGAWKVHSVIVJXJWHXKAKWACTEWBATEWCBTEWHXKVPCVKAVQBVQCABVLVMVNVOV
    RVSVT $.
    $( [31-Mar-2011] $)

  $( Special case of ~ ndvdsadd .  If an integer ` D ` greater than ` 1 `
     divides ` N ` , it does not divide ` N + 1 ` .  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  ndvdsp1 $p |- ( ( N e. ZZ /\ D e. NN /\ 1 < D ) ->
                  ( D || N -> -. D || ( N + 1 ) ) ) $=
    ( c1 clt wbr cz wcel cn wa cdivides caddc co wn 1nn jctl ndvdsadd syl3an3
    wi ) CADEZBFGAHGCHGZSIABJEABCKLJEMRSTNOACBPQ $.
    $( [31-Mar-2011] $)

  ${
    ndvdsi.1 $e |- A e. NN $.
    ndvdsi.2 $e |- Q e. NN0 $.
    ndvdsi.3 $e |- R e. NN $.
    ndvdsi.4 $e |- B = ( ( A x. Q ) + R ) $.
    ndvdsi.5 $e |- R < A $.
    $( A quick test for non-divisibility.  (Contributed by Mario Carneiro,
       18-Feb-2014.) $)
    ndvdsi $p |- -. A || B $=
      ( cdivides wbr cmul co caddc wn cz wcel nnzi mp2an cn dvdsmul1 clt zmulcl
      nn0zi wa wi pm3.2i ndvdsadd mp3an ax-mp breq2i mtbir ) ABJKAACLMZDNMZJKZA
      UMJKZUOOZAPQZCPQZUPAERZCFUDZACUASUMPQZATQDTQZDAUBKZUEUPUQUFURUSVBUTVAACUC
      SEVCVDGIUGADUMUHUIUJBUNAJHUKUL $.
      $( [18-Feb-2014] $)
  $}

