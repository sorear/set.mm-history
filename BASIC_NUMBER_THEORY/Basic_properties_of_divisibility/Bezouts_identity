$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                           B&eacute;zout's identity
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d s t u v x y z A $.  $d s t u v x y z B $.
    ${
      $d s t u v x y z G $.  $d s t u v M $.  $d s t u v x y z ph $.
      $d s t u v x y z C $.
      bezout.1 $e |- M = { z e. NN | E. x e. ZZ E. y e. ZZ
                           z = ( ( A x. x ) + ( B x. y ) ) } $.
      bezout.3 $e |- ( ph -> A e. ZZ ) $.
      bezout.4 $e |- ( ph -> B e. ZZ ) $.
      $( Lemma for ~ bezout . $)
      bezoutlem1 $p |- ( ph -> ( A =/= 0 -> ( abs ` A ) e. M ) ) $=
        ( cmul co wceq cz wrex cc0 wcel c1 cc syl cabs cfv cv caddc fveq2 oveq1
        wne cn eqeq12d rexbidv cr zre cneg wo absor ax-1rid eqcomd oveq2 eqeq2d
        1z rcla4ev sylancr eqeq1 syl5ibrcom znegcl ax-mp recn zcn mulcom eqtr3d
        mulm1 jaod mpd vtoclga wa adantr mul01 oveq2d mulcl syl2an addid1 eqtrd
        0z mpan syl6bir reximdva wi nnabscl 2rexbidv elrab2 simplbi2com sylsyld
        ex ) AEUAUBZEBUCZKLZFCUCZKLZUDLZMZCNOZBNOZEPUGZWNUHQZWNGQZAWNWPMZBNOZXB
        AENQZXGIDUCZUAUBZXIWOKLZMZBNOZXGDENXIEMZXLXFBNXNXJWNXKWPXIEUAUEXIEWOKUF
        UIUJXINQXIUKQZXMXIULXOXJXIMZXJXIUMZMZUNXMXIUOXOXPXMXRXOXMXPXIXKMZBNOZXO
        RNQZXIXIRKLZMZXTUTXOYBXIXIUPUQXSYCBRNWORMXKYBXIWORXIKURUSVAVBXPXLXSBNXJ
        XIXKVCUJVDXOXMXRXQXKMZBNOZXORUMZNQZXQXIYFKLZMZYEYAYGUTRVEVFZXOYFXIKLZXQ
        YHXOXISQZYKXQMXIVGZXIVKTXOYFSQZYLYKYHMYGYNYJYFVHVFYMYFXIVIVBVJYDYIBYFNW
        OYFMXKYHXQWOYFXIKURUSVAVBXRXLYDBNXJXQXKVCUJVDVLVMTVNTAXFXABNAWONQZVOZXF
        WNWPFPKLZUDLZMZXAYPYRWPWNYPYRWPPUDLZWPYPYQPWPUDYPFSQZYQPMAUUAYOAFNQUUAJ
        FVHTVPFVQTVRYPWPSQZYTWPMAESQZWOSQUUBYOAXHUUCIEVHTWOVHEWOVSVTWPWATWBUSPN
        QYSXAWCWTYSCPNWQPMZWSYRWNUUDWRYQWPUDWQPFKURVRUSVAWDWEWFVMAXHXCXDWGIXHXC
        XDEWHWMTXEXDXBXIWSMZCNOBNOXBDWNUHGXIWNMUUEWTBCNNXIWNWSVCWIHWJWKWL $.
        $( [15-Mar-2014] $)

      bezout.2 $e |- G = sup ( M , RR , `' < ) $.
      bezout.5 $e |- ( ph -> -. ( A = 0 /\ B = 0 ) ) $.
      $( Lemma for ~ bezout . $)
      bezoutlem2 $p |- ( ph -> G e. M ) $=
        ( wcel cn wceq cz wrex cc0 cc cr clt ccnv csup c1 cuz cfv wss c0 wne cv
        cmul co caddc crab ssrab2 eqsstri nnuz sseqtri wo wa neorian bezoutlem1
        sylibr cabs ne0i syl6 eqid rexcom zcn syl adantr ad2antll mulcl syl2anc
        ad2antrl addcom eqeq2d 2rexbidva syl5bb rabbidv syl5eq sylibrd jaod mpd
        wn eleq2d infmssuzcl sylancr syl5eqel ) AGHUAUBUCUDZHLAHUEUFUGZUHHUIUJZ
        WKHNHOWLHDUKZEBUKZULUMZFCUKZULUMZUNUMZPZCQRBQRZDOUOZOIXADOUPUQURUSAESUJ
        ZFSUJZUTZWMAESPFSPVAWFXEMESFSVBVDAXCWMXDAXCEVEUGZHNWMABCDEFHIJKVCHXFVFV
        GAXDFVEUGZHNZWMAXDXGWNWRWPUNUMZPZBQRCQRZDOUOZNXHACBDFEXLXLVHKJVCAHXLXGA
        HXBXLIAXAXKDOXAWTBQRCQRAXKWTBCQQVIAWTXJCBQQAWQQNZWOQNZVAZVAZWSXIWNXPWPT
        NZWRTNZWSXIPXPETNZWOTNZXQAXSXOAEQNXSJEVJVKVLXNXTAXMWOVJVMEWOVNVOXPFTNZW
        QTNZXRAYAXOAFQNYAKFVJVKVLXMYBAXNWQVJVPFWQVNVOWPWRVQVOVRVSVTWAWBWGWCHXGV
        FVGWDWEHUEWHWIWJ $.
        $( [15-Mar-2014] $)

      $d M x y $.
      $( Lemma for ~ bezout . $)
      bezoutlem3 $p |- ( ph -> ( C e. M -> G || C ) ) $=
        ( wcel co wceq cmul caddc cz vs vt vu vv cdivides wbr wa cmo cc0 cn cle
        wn clt cr crp cv wrex simpr eqeq1 2rexbidv oveq1d eqeq2d rexbidv oveq2d
        oveq2 syl6bb elrab2 sylib simpld nnre syl bezoutlem2 syl5bb nnrp adantr
        cbvrexv modlt syl2anc wb cn0 nnz zmodcl nn0re ltnle mpbid cdiv cfl cmin
        cfv simprd ad2antrr simprll simprrl nndivre flcl zmulcl simprlr simprrr
        wi zsubcl cc mulcl addsub4 syl22anc adddir syl3anc mulass oveq12d eqtrd
        subdi 3eqtr4d rcla42ev oveq1 oveq12 sylan2 eqeq1d syl5ibrcom exp3acom23
        zcn expr rexlimdvv mpd modval eqcomd simplbi2com ccnv csup cuz wss crab
        ex c1 ssrab2 eqsstri nnuz sseqtri infmssuzle mpan syl5eqbr syl6 mtod wo
        elnn0 ord divides3 mpbird ) AGIOZHGUEUFZAUUGUGZUUHGHUHPZUIQZUUIUUJUJOZU
        LUUKUUIUULHUUJUKUFZUUIUUJHUMUFZUUMULZUUIGUNOZHUOOZUUNUUIGUJOZUUPUUIUURG
        EUAUPZRPZFUBUPZRPZSPZQZUBTUQZUATUQZUUIUUGUURUVFUGAUUGURDUPZEBUPZRPZFCUP
        ZRPZSPZQZCTUQZBTUQZUVFDGUJIUVGGQZUVOGUVLQZCTUQZBTUQUVFUVPUVMUVQBCTTUVGG
        UVLUSUTUVRUVEBUATUVHUUSQZUVRGUUTUVKSPZQZCTUQUVEUVSUVQUWACTUVSUVLUVTGUVS
        UVIUUTUVKSUVHUUSERVEVAVBVCUWAUVDCUBTUVJUVAQZUVTUVCGUWBUVKUVBUUTSUVJUVAF
        RVEVDVBVPVFVPVFJVGVHZVIZGVJVKZAUUQUUGAHUJOZUUQAUWFHEUCUPZRPZFUDUPZRPZSP
        ZQZUDTUQUCTUQZAHIOUWFUWMUGABCDEFHIJKLMNVLUVOUWMDHUJIUVOUVGUWKQZUDTUQZUC
        TUQUVGHQZUWMUVNUWOBUCTUVHUWGQZUVNUVGUWHUVKSPZQZCTUQUWOUWQUVMUWSCTUWQUVL
        UWRUVGUWQUVIUWHUVKSUVHUWGERVEVAVBVCUWSUWNCUDTUVJUWIQZUWRUWKUVGUWTUVKUWJ
        UWHSUVJUWIFRVEVDVBVPVFVPUWPUWNUWLUCUDTTUVGHUWKUSUTVMJVGVHZVIZHVNVKVOZGH
        VQVRUUIUUJUNOZHUNOZUUNUUOVSUUIUUJVTOZUXDUUIGTOZUWFUXFUUIUURUXGUWDGWAVKZ
        AUWFUUGUXBVOZGHWBVRZUUJWCVKAUXEUUGAUWFUXEUXBHVJVKVOUUJHWDVRWEUUIUULUUJI
        OZUUMUUIUUJUVLQZCTUQBTUQZUULUXKWSUUIGHGHWFPZWGWIZRPZWHPZUVLQZCTUQBTUQZU
        XMUUIUVFUXSUUIUURUVFUWCWJUUIUVDUXSUAUBTTUUIUUSTOZUVATOZUGZUVDUXSWSZUUIU
        YBUGZUWMUYCAUWMUUGUYBAUWFUWMUXAWJWKUYDUWLUYCUCUDTTUUIUYBUWGTOZUWITOZUGZ
        UWLUYCWSUUIUYBUYGUGZUGZUVDUWLUXSUYIUXSUVDUWLUGZUVCUWKUXORPZWHPZUVLQZCTU
        QBTUQZUYIUUSUWGUXORPZWHPZTOZUVAUWIUXORPZWHPZTOZUYLEUYPRPZFUYSRPZSPZQZUY
        NUYIUXTUYOTOZUYQUUIUXTUYAUYGWLZUYIUYEUXOTOZVUEUUIUYBUYEUYFWMZUUIVUGUYHU
        UIUXNUNOZVUGUUIUUPUWFVUIUWEUXIGHWNVRUXNWOVKZVOZUWGUXOWPVRZUUSUYOWTVRUYI
        UYAUYRTOZUYTUUIUXTUYAUYGWQZUYIUYFVUGVUMUUIUYBUYEUYFWRZVUKUWIUXOWPVRZUVA
        UYRWTVRUYIUVCEUYORPZFUYRRPZSPZWHPZUUTVUQWHPZUVBVURWHPZSPZUYLVUCUYIUUTXA
        OZUVBXAOZVUQXAOZVURXAOZVUTVVCQUYIEXAOZUUSXAOZVVDAVVHUUGUYHAETOVVHKEXSVK
        WKZUYIUXTVVIVUFUUSXSVKZEUUSXBVRUYIFXAOZUVAXAOZVVEAVVLUUGUYHAFTOVVLLFXSV
        KWKZUYIUYAVVMVUNUVAXSVKZFUVAXBVRUYIVVHUYOXAOZVVFVVJUYIVUEVVPVULUYOXSVKZ
        EUYOXBVRUYIVVLUYRXAOZVVGVVNUYIVUMVVRVUPUYRXSVKZFUYRXBVRUUTUVBVUQVURXCXD
        UYIUYKVUSUVCWHUYIUYKUWHUXORPZUWJUXORPZSPZVUSUYIUWHXAOZUWJXAOZUXOXAOZUYK
        VWBQUYIVVHUWGXAOZVWCVVJUYIUYEVWFVUHUWGXSVKZEUWGXBVRUYIVVLUWIXAOZVWDVVNU
        YIUYFVWHVUOUWIXSVKZFUWIXBVRUUIVWEUYHUUIVUGVWEVUJUXOXSVKVOZUWHUWJUXOXEXF
        UYIVVTVUQVWAVURSUYIVVHVWFVWEVVTVUQQVVJVWGVWJEUWGUXOXGXFUYIVVLVWHVWEVWAV
        URQVVNVWIVWJFUWIUXOXGXFXHXIVDUYIVUAVVAVUBVVBSUYIVVHVVIVVPVUAVVAQVVJVVKV
        VQEUUSUYOXJXFUYIVVLVVMVVRVUBVVBQVVNVVOVVSFUVAUYRXJXFXHXKUYMVUDUYLVUAUVK
        SPZQBCUYPUYSTTUVHUYPQZUVLVWKUYLVWLUVIVUAUVKSUVHUYPERVEVAVBUVJUYSQZVWKVU
        CUYLVWMUVKVUBVUASUVJUYSFRVEVDVBXLXFUYJUXRUYMBCTTUYJUXQUYLUVLUWLUVDUXPUY
        KQUXQUYLQHUWKUXORXMGUVCUXPUYKWHXNXOXPUTXQXRXTYAYBYKYAYBUUIUXRUXLBCTTUUI
        UXQUUJUVLUUIUUJUXQUUIUUPUUQUUJUXQQUWEUXCGHYCVRYDXPUTWEUXKUULUXMUVOUXMDU
        UJUJIUVGUUJQUVMUXLBCTTUVGUUJUVLUSUTJVGYEVKUXKHIUNUMYFYGZUUJUKMIYLYHWIZY
        IUXKVWNUUJUKUFIUJVWOIUVODUJYJUJJUVODUJYMYNYOYPUUJIYLYQYRYSYTUUAUUIUULUU
        KUUIUXFUULUUKUUBUXJUUJUUCVHUUDYBUUIUWFUXGUUHUUKVSUXIUXHHGUUEVRUUFYK $.
        $( [22-Feb-2014] $)

      $( Lemma for ~ bezout . $)
      bezoutlem4 $p |- ( ph -> ( A gcd B ) e. M ) $=
        ( co wceq wcel cz syl2anc cmul cdivides vu vv vs vt cgcd cle wbr cr cn0
        wa wb gcdcl nn0re syl cn caddc wrex bezoutlem2 weq oveq2 oveq1d rexbidv
        cv eqeq2d oveq2d cbvrexv syl6bb eqeq1 2rexbidv syl5bb elrab2 sylib nnre
        simpld letri3 gcddvds nn0z divides simprd reeanv simprrl simprll zmulcl
        mpbid wi simprrr simprlr zaddcl adantr dvdsmul2 cc adddir syl3anc mul32
        zcn oveq12d eqtrd breqtrd oveq1 oveqan12d breq2d syl5ibcom breq2 imbi2d
        syl5ibrcom expr ex rexlimdvv mpd syl5bir mp2and dvdsle cc0 wne cabs cfv
        com23 bezoutlem1 bezoutlem3 syld nnz dvdsabsb sylibrd imp 3syl pm2.61ne
        dvds0 crab eqid rexcom ad2antll mulcl ad2antrl addcom 2rexbidva rabbidv
        syl5eq eleq2d wn dvdslegcd syl31anc mpbir2and eqeltrd ) AEFUENZGHAUUDGO
        ZUUDGUFUGZGUUDUFUGZAUUDUHPZGUHPZUUEUUFUUGUJUKAUUDUIPZUUHAEQPZFQPZUUJJKE
        FULRZUUDUMUNAGUOPZUUIAUUNGEUAVCZSNZFUBVCZSNZUPNZOZUBQUQUAQUQZAGHPUUNUVA
        UJABCDEFGHIJKLMURZDVCZEBVCZSNZFCVCZSNZUPNZOZCQUQZBQUQZUVADGUOHUVKUVCUUS
        OZUBQUQZUAQUQUVCGOZUVAUVJUVMBUAQBUAUSZUVJUVCUUPUVGUPNZOZCQUQUVMUVOUVIUV
        QCQUVOUVHUVPUVCUVOUVEUUPUVGUPUVDUUOESUTVAVDVBUVQUVLCUBQCUBUSZUVPUUSUVCU
        VRUVGUURUUPUPUVFUUQFSUTVEVDVFVGVFUVNUVLUUTUAUBQQUVCGUUSVHVIVJIVKVLZVNZG
        VMUNUUDGVORAUUDGTUGZUUFAUCVCZUUDSNZEOZUCQUQZUDVCZUUDSNZFOZUDQUQZUWAAUUD
        ETUGZUWEAUWJUUDFTUGZAUUKUULUWJUWKUJJKEFVPRZVNAUUDQPZUUKUWJUWEUKAUUJUWMU
        UMUUDVQUNZJUCUUDEVRRWDAUWKUWIAUWJUWKUWLVSAUWMUULUWKUWIUKUWNKUDUUDFVRRWD
        UWEUWIUJUWDUWHUJZUDQUQUCQUQAUWAUWDUWHUCUDQQVTAUWOUWAUCUDQQAUVAUWBQPZUWF
        QPZUJZUWOUWAWEZWEZAUUNUVAUVSVSAUUTUWTUAUBQQAUUOQPZUUQQPZUJZUUTUWTWEAUXC
        UJUWRUUTUWSAUXCUWRUUTUWSWEAUXCUWRUJZUJZUWSUUTUWOUUDUUSTUGZWEUXEUUDUWCUU
        OSNZUWGUUQSNZUPNZTUGUWOUXFUXEUUDUWBUUOSNZUWFUUQSNZUPNZUUDSNZUXITUXEUXLQ
        PZUWMUUDUXMTUGUXEUXJQPZUXKQPZUXNUXEUWPUXAUXOAUXCUWPUWQWAZAUXAUXBUWRWBZU
        WBUUOWCRZUXEUWQUXBUXPAUXCUWPUWQWFZAUXAUXBUWRWGZUWFUUQWCRZUXJUXKWHRAUWMU
        XDUWNWIZUXLUUDWJRUXEUXMUXJUUDSNZUXKUUDSNZUPNZUXIUXEUXJWKPZUXKWKPZUUDWKP
        ZUXMUYFOUXEUXOUYGUXSUXJWOUNUXEUXPUYHUYBUXKWOUNUXEUWMUYIUYCUUDWOUNZUXJUX
        KUUDWLWMUXEUYDUXGUYEUXHUPUXEUWBWKPZUUOWKPZUYIUYDUXGOUXEUWPUYKUXQUWBWOUN
        UXEUXAUYLUXRUUOWOUNUYJUWBUUOUUDWNWMUXEUWFWKPZUUQWKPZUYIUYEUXHOUXEUWQUYM
        UXTUWFWOUNUXEUXBUYNUYAUUQWOUNUYJUWFUUQUUDWNWMWPWQWRUWOUXIUUSUUDTUWDUWHU
        XGUUPUXHUURUPUWCEUUOSWSUWGFUUQSWSWTXAXBUUTUWAUXFUWOGUUSUUDTXCXDXEXFXQXG
        XHXIXHXJXKAUWMUUNUWAUUFWEUWNUVTUUDGXLRXIAGETUGZGFTUGZUUGAUYOGXMTUGZEXME
        XMGTXCAEXMXNZUYOAUYRGEXOXPZTUGZUYOAUYRUYSHPUYTABCDEFHIJKXRABCDEFUYSGHIJ
        KLMXSXTAGQPZUUKUYOUYTUKAUUNVUAUVTGYAZUNZJGEYBRYCYDAUUNVUAUYQUVTVUBGYGYE
        ZYFAUYPUYQFXMFXMGTXCAFXMXNZUYPAVUEGFXOXPZTUGZUYPAVUEVUFHPZVUGAVUEVUFUVC
        UVGUVEUPNZOZBQUQCQUQZDUOYHZPVUHACBDFEVULVULYIKJXRAHVULVUFAHUVKDUOYHVULI
        AUVKVUKDUOUVKUVIBQUQCQUQAVUKUVIBCQQYJAUVIVUJCBQQAUVFQPZUVDQPZUJZUJZUVHV
        UIUVCVUPUVEWKPZUVGWKPZUVHVUIOVUPEWKPZUVDWKPZVUQAVUSVUOAUUKVUSJEWOUNWIVU
        NVUTAVUMUVDWOYKEUVDYLRVUPFWKPZUVFWKPZVURAVVAVUOAUULVVAKFWOUNWIVUMVVBAVU
        NUVFWOYMFUVFYLRUVEUVGYNRVDYOVJYPYQYRYCABCDEFVUFGHIJKLMXSXTAVUAUULUYPVUG
        UKVUCKGFYBRYCYDVUDYFAVUAUUKUULEXMOFXMOUJYSUYOUYPUJUUGWEVUCJKMGEFYTUUAXK
        UUBUVBUUC $.
        $( [22-Feb-2014] $)
    $}

    $( B&eacute;zout's identity:  For any integers ` A ` and ` B ` , there are
       integers ` x , y ` such that ` ( A gcd B ) = A x. x + B x. y ` .
       (Contributed by Mario Carneiro, 22-Feb-2014.) $)
    bezout $p |- ( ( A e. ZZ /\ B e. ZZ ) ->
           E. x e. ZZ E. y e. ZZ ( A gcd B ) = ( ( A x. x ) + ( B x. y ) ) ) $=
      ( vz vu vv vt cz wcel cc0 wceq co cv cmul caddc wrex cn oveq2 eqeq2d cgcd
      wa wn crab clt ccnv csup weq eqeq1 2rexbidv oveq1d rexbidv oveq2d cbvrexv
      cr syl6bb cbvrabv simpll simplr eqid simpr bezoutlem4 elrab simprbi ex 0z
      syl 00id 0cn mul01i oveq12i gcd0val 3eqtr4ri mp3an oveq12 oveq1 oveqan12d
      rcla42ev eqeq12d mpbiri pm2.61d2 ) CIJZDIJZUBZCKLZDKLZUBZCDUAMZCANZOMZDBN
      ZOMZPMZLZBIQAIQZWDWGUCZWOWDWPUBZWHENZWMLZBIQAIQZERUDZJZWOWQFGHCDXAUOUEUFU
      GZXAWTHNZCFNZOMZDGNZOMZPMZLZGIQZFIQZEHREHUHZWTXDWMLZBIQZAIQXLXMWSXNABIIWR
      XDWMUIUJXOXKAFIAFUHZXOXDXFWLPMZLZBIQXKXPXNXRBIXPWMXQXDXPWJXFWLPWIXECOSUKT
      ULXRXJBGIBGUHZXQXIXDXSWLXHXFPWKXGDOSUMTUNUPUNUPUQWBWCWPURWBWCWPUSXCUTWDWP
      VAVBXBWHRJWOWTWOEWHRWRWHLWSWNABIIWRWHWMUIUJVCVDVGVEWGWOKKUAMZKWIOMZKWKOMZ
      PMZLZBIQAIQZKIJZYFXTKKOMZYGPMZLZYEVFVFKKPMKYHXTVHYGKYGKPKVIVJZYJVKVLVMYDY
      IXTYGYBPMZLABKKIIWIKLZYCYKXTYLYAYGYBPWIKKOSUKTWKKLZYKYHXTYMYBYGYGPWKKKOSU
      MTVRVNWGWNYDABIIWGWHXTWMYCCKDKUAVOWEWFWJYAWLYBPCKWIOVPDKWKOVPVQVSUJVTWA
      $.
      $( [22-Feb-2014] $)
  $}

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( An integer which divides each of two others also divides their gcd.
       (Contributed by Paul Chapman, 22-Jun-2011.)  (Revised by Mario Carneiro,
       30-May-2014.) $)
    dvdsgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M gcd N ) ) ) $=
      ( vx vy cz wcel w3a cdivides wbr wa co cv cmul caddc wceq wrex wi cc zcn
      bezout 3adant1 dvds2ln 3impia simp3l simp12 mulcom syl2an syl2anc oveq12d
      cgcd 3coml simp3r simp13 breqtrd breq2 syl5ibrcom 3expia rexlimdvv mpid
      ex ) AFGZBFGZCFGZHZABIJACIJKZBCUKLZBDMZNLZCEMZNLZOLZPZEFQDFQZAVGIJZVCVDVN
      VBDEBCUAUBVEVFVNVORVEVFKVMVODEFFVEVFVHFGZVJFGZKZVMVORVEVFVRHZVOVMAVLIJVSA
      VHBNLZVJCNLZOLZVLIVRVEVFAWBIJZVRVEVFWCVHVJABCUCUDULVSVTVIWAVKOVSVPVCVTVIP
      ZVEVFVPVQUEVBVCVDVFVRUFVPVHSGBSGWDVCVHTBTVHBUGUHUIVSVQVDWAVKPZVEVFVPVQUMV
      BVCVDVFVRUNVQVJSGCSGWEVDVJTCTVJCUGUHUIUJUOVGVLAIUPUQURUSVAUT $.
      $( [30-May-2014] $) $( [22-Jun-2011] $)
  $}

  $( Biconditional form of ~ dvdsgcd .  (Contributed by Scott Fenton,
     02-Apr-2014.) $)
  dvdsgcdb $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
    ( ( K || M /\ K || N ) <-> K || ( M gcd N ) ) ) $=
    ( cz wcel w3a cdivides wbr wa cgcd co dvdsgcd gcddvds simpld 3adant1 wi cn0
    simp1 dvdstr mpan2d gcdcl nn0z simp2 syl3anc simprd syld3an2 jcad impbid
    syl ) ADEZBDEZCDEZFZABGHZACGHZIABCJKZGHZABCLUMUQUNUOUMUQUPBGHZUNUKULURUJUKU
    LIZURUPCGHZBCMZNOUMUJUPDEZUKUQURIUNPUJUKULRUKULVBUJUSUPQEVBBCUAUPUBUIOZUJUK
    ULUCAUPBSUDTUMUQUTUOUKULUTUJUSURUTVAUEOUJVBUKULUQUTIUOPVCAUPCSUFTUGUH $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  ${
    $d N x $.  $d M x $.  $d P x $.
    $( Associative law for ` gcd ` operator.  (Contributed by Scott Fenton,
       02-Apr-2014.) $)
    gcdass $p |- ( ( N e. ZZ /\ M e. ZZ /\ P e. ZZ ) ->
    ( ( N gcd M ) gcd P ) = ( N gcd ( M gcd P ) ) ) $=
      ( vx cz wcel cc0 wceq wa cdivides wbr crab cr clt csup cif cgcd co anass
      wb w3a cv a1i rabbiia supeq1i ifbieq2i cn0 gcdcl 3adant3 syl simp3 gcdval
      nn0z syl2anc gcdeq0 anbi1d bicomd simpl1 simpl2 dvdsgcdb syl3anc rabbidva
      simpr supeq1d ifbieq2d eqtr4d simp1 3adant1 anbi2d simpl3 3eqtr4a ) CEFZB
      EFZAEFZUAZCGHZBGHZIZAGHZIZGDUBZCJKZWABJKZIZWAAJKZIZDELZMNOZPZVPVQVSIZIZGW
      BWCWEIZIZDELZMNOZPZCBQRZAQRZCBAQRZQRZVTWKWHWOGVPVQVSSMWGWNNWFWMDEWFWMTWAE
      FZWBWCWESUCUDUEUFVOWRWQGHZVSIZGWAWQJKZWEIZDELZMNOZPZWIVOWQEFZVNWRXHHVOWQU
      GFZXIVLVMXJVNCBUHUIWQUMUJVLVMVNUKDWQAULUNVOVTXCWHXGGVOXCVTVOXBVRVSVLVMXBV
      RTVNCBUOUIUPUQVOMWGXFNVOWFXEDEVOXAIZWDXDWEXKXAVLVMWDXDTVOXAVCZVLVMVNXAURV
      LVMVNXAUSZWACBUTVAUPVBVDVEVFVOWTVPWSGHZIZGWBWAWSJKZIZDELZMNOZPZWPVOVLWSEF
      ZWTXTHVLVMVNVGVOWSUGFZYAVMVNYBVLBAUHVHWSUMUJDCWSULUNVOWKXOWOXSGVOXOWKVOXN
      WJVPVMVNXNWJTVLBAUOVHVIUQVOMWNXRNVOWMXQDEXKWLXPWBXKXAVMVNWLXPTXLXMVLVMVNX
      AVJWABAUTVAVIVBVDVEVFVK $.
      $( [19-Apr-2014] $) $( [2-Apr-2014] $)
  $}

  $( Distribute multiplication by a nonnegative integer over gcd.  (Contributed
     by Paul Chapman, 22-Jun-2011.)  (The proof was shortened by Mario
     Carneiro, 30-May-2014.) $)
  mulgcd $p |- ( ( K e. NN0 /\ M e. ZZ /\ N e. ZZ ) ->
                 ( ( K x. M ) gcd ( K x. N ) ) = ( K x. ( M gcd N ) ) ) $=
    ( cn0 wcel cz cmul co cgcd wceq cc0 wa cdivides wbr syl2anc 3adant1 syl3anc
    wi syl cc cn wo elnn0 w3a simp1 nnz simp2 zmulcl simp3 gcdcl nnnn0 nn0mulcl
    cdiv wne nn0cn nncn nnne0 divcan2 gcddvds simpld eqbrtrd wb dvdsmul1 mp2and
    nn0z divides2 mpbid dvdscmulr syl112anc simprd dvdscmul mpd eqbrtrrd dvdseq
    dvdsgcd syl22anc 3expib mul02 gcd0val syl6reqr oveq1d zcn 3ad2ant2 3ad2ant3
    eqtrd oveq12d 3eqtr4d jaoi sylbi 3impib ) ADEZBFEZCFEZABGHZACGHZIHZABCIHZGH
    ZJZWKAUAEZAKJZUBWLWMLWSRZAUCWTXBXAWTWLWMWSWTWLWMUDZWPDEZWRDEZWPWRMNWRWPMNZW
    SXCWNFEZWOFEZXDXCAFEZWLXGXCWTXIWTWLWMUEZAUFSZWTWLWMUGZABUHOZXCXIWMXHXKWTWLW
    MUIZACUHOZWNWOUJOZXCWKWQDEZXEXCWTWKXJAUKSWLWMXQWTBCUJZPZAWQULOZXCAWPAUMHZGH
    ZWPWRMXCWPTEZATEZAKUNZYBWPJXCXDYCXPWPUOSXCWTYDXJAUPSXCWTYEXJAUQSZWPAURQZXCY
    AWQMNZYBWRMNZXCYABMNZYACMNZYHXCYBWNMNZYJXCYBWPWNMYGXCWPWNMNZWPWOMNZXCXGXHYM
    YNLXMXOWNWOUSOZUTVAXCYAFEZWLXIYEYLYJVBXCAWPMNZYPXCAWNMNZAWOMNZYQXCXIWLYRXKX
    LABVCOXCXIWMYSXKXNACVCOXCXIXGXHYRYSLYQRXKXMXOAWNWOVOQVDXCXIYEWPFEZYQYPVBXKY
    FXCXDYTXPWPVESAWPVFQVGZXLXKYFAYABVHVIVGXCYBWOMNZYKXCYBWPWOMYGXCYMYNYOVJVAXC
    YPWMXIYEUUBYKVBUUAXNXKYFAYACVHVIVGXCYPWLWMYJYKLYHRUUAXLXNYABCVOQVDXCYPWQFEZ
    XIYHYIRUUAXCXQUUCXSWQVESZXKAYAWQVKQVLVMXCWRWNMNZWRWOMNZXFXCWQBMNZUUEXCUUGWQ
    CMNZWLWMUUGUUHLWTBCUSPZUTXCUUCWLXIUUGUUERUUDXLXKAWQBVKQVLXCUUHUUFXCUUGUUHUU
    IVJXCUUCWMXIUUHUUFRUUDXNXKAWQCVKQVLXCWRFEZXGXHUUEUUFLXFRXCXEUUJXTWRVESXMXOW
    RWNWOVOQVDWPWRVNVPVQXAWLWMWSXAWLWMUDZKKIHZKWQGHZWPWRUUKUUMKUULUUKWQTEZUUMKJ
    UUKXQUUNWLWMXQXAXRPWQUOSWQVRSVSVTUUKWNKWOKIUUKWNKBGHZKUUKAKBGXAWLWMUEZWAUUK
    BTEZUUOKJWLXAUUQWMBWBWCBVRSWEUUKWOKCGHZKUUKAKCGUUPWAUUKCTEZUURKJWMXAUUSWLCW
    BWDCVRSWEWFUUKAKWQGUUPWAWGVQWHWIWJ $.
    $( [30-May-2014] $) $( [22-Jun-2011] $)

  $( Distribute absolute value of multiplication over gcd.  (Contributed by
     Paul Chapman, 22-Jun-2011.) $)
  absmulgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
      ( ( K x. M ) gcd ( K x. N ) ) = ( abs ` ( K x. ( M gcd N ) ) ) ) $=
    ( cz wcel cabs cfv cgcd co cmul wceq wa cn0 syl oveq2d 3adant1 zcn nn0abscl
    cc absmul w3a gcdcl cc0 cle wbr nn0re nn0ge0 absid syl2anc syl2an oveqan12d
    nn0cn 3impb 3impdi syl3an zmulcl gcdabs nn0z mulgcd 3eqtr3d eqtrd 3eqtr4rd
    cr ) ADEZBDEZCDEZUAZAFGZBCHIZFGZJIZVHVIJIZAVIJIFGZABJIZACJIZHIZVEVFVKVLKVDV
    EVFLZVJVIVHJVQVIMEZVJVIKZBCUBZVRVIVCEUCVIUDUEVSVIUFVIUGVIUHUINOPVDVEVFVMVKK
    ZVDASEZVISEZWAVQAQZVQVRWCVTVIULNAVITUJUMVGVPVHBFGZCFGZHIZJIZVLVGVNFGZVOFGZH
    IZVHWEJIZVHWFJIZHIZVPWHVDWBVEBSEZVFCSEZWKWNKZWDBQCQWBWOWPWQWBWOLWBWPLWIWLWJ
    WMHABTACTUKUNUOVDVEVFWKVPKZVDVELVNDEVODEWRVDVFLABUPACUPVNVOUQUJUNVDVHMEVEWE
    DEZVFWFDEZWNWHKARVEWEMEWSBRWEURNVFWFMEWTCRWFURNVHWEWFUSUOUTVGWGVIVHJVEVFWGV
    IKVDBCUQPOVAVB $.
    $( [22-Jun-2011] $)

  $( Reverse distribution law for the ` gcd ` operator.  (Contributed by Scott
     Fenton, 02-Apr-2014.) $)
  mulgcdr $p |- ( ( A e. ZZ /\ B e. ZZ /\ C e. NN0 ) ->
    ( ( A x. C ) gcd ( B x. C ) ) = ( ( A gcd B ) x. C ) ) $=
    ( cz wcel cn0 w3a cmul co cgcd wceq mulgcd 3coml cc 3ad2ant1 nn0cn 3ad2ant3
    zcn mulcom syl2anc 3ad2ant2 oveq12d gcdcl 3adant3 syl 3eqtr4d ) ADEZBDEZCFE
    ZGZCAHIZCBHIZJIZCABJIZHIZACHIZBCHIZJIUNCHIZUIUGUHUMUOKCABLMUJUPUKUQULJUJANE
    ZCNEZUPUKKUGUHUSUIAROUIUGUTUHCPQZACSTUJBNEZUTUQULKUHUGVBUIBRUAVABCSTUBUJUNN
    EZUTURUOKUJUNFEZVCUGUHVDUIABUCUDUNPUEVAUNCSTUF $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  ${
    $d A a b $.  $d B a b $.  $d C a b $.
    $( Division law for GCD. (Contributed by Scott Fenton, 18-Apr-2014.) $)
    gcddiv $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. NN )
       /\ ( C || A /\ C || B ) ) ->
       ( ( A gcd B ) / C ) = ( ( A / C ) gcd ( B / C ) ) ) $=
      ( va vb cz wcel w3a cdivides wa cgcd cdiv wceq cmul wrex 3ad2ant3 divcan4
      co cc syl3anc cn wbr cv wb nnz simp1 divides syl2anc simp2 anbi12d reeanv
      syl6bbr wi cc0 wne cn0 gcdcl nn0cn syl 3adant3 nncn nnne0 mulgcdr syl3an3
      nnnn0 oveq1d zcn 3ad2ant1 3ad2ant2 oveq12d 3eqtr4d oveq12 oveq1 oveqan12d
      eqeq12d syl5ibcom 3expa expcom rexlimdvv sylbid imp ) AFGZBFGZCUAGZHZCAIU
      BZCBIUBZJZABKRZCLRZACLRZBCLRZKRZMZWEWHDUCZCNRZAMZEUCZCNRZBMZJZEFODFOZWNWE
      WHWQDFOZWTEFOZJXBWEWFXCWGXDWECFGZWBWFXCUDWDWBXEWCCUEPZWBWCWDUFDCAUGUHWEXE
      WCWGXDUDXFWBWCWDUIECBUGUHUJWQWTDEFFUKULWDWBXBWNUMWCWDXAWNDEFFWOFGZWRFGZJZ
      WDXAWNUMZXGXHWDXJXGXHWDHZWPWSKRZCLRZWPCLRZWSCLRZKRZMXAWNXKWOWRKRZCNRZCLRZ
      XQXMXPXKXQSGZCSGZCUNUOZXSXQMXGXHXTWDXIXQUPGXTWOWRUQXQURUSUTWDXGYAXHCVAPZW
      DXGYBXHCVBPZXQCQTXKXLXRCLWDXGXHCUPGXLXRMCVEWOWRCVCVDVFXKXNWOXOWRKXKWOSGZY
      AYBXNWOMXGXHYEWDWOVGVHYCYDWOCQTXKWRSGZYAYBXOWRMXHXGYFWDWRVGVIYCYDWRCQTVJV
      KXAXMWJXPWMXAXLWICLWPAWSBKVLVFWQWTXNWKXOWLKWPACLVMWSBCLVMVNVOVPVQVRVSPVTW
      A $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d M k n $.  $d N k n $.
    $( The GCD of a multiple of a number is the number itself.  (Contributed by
       Scott Fenton, 12-Apr-2014.) $)
    gcdmultiple $p |- ( ( M e. NN /\ N e. NN ) -> ( M gcd ( M x. N ) ) = M ) $=
      ( vk vn cn wcel cmul co cgcd wceq wi caddc oveq2 oveq2d eqeq1d imbi2d syl
      c1 cc cz cv weq cabs cfv nncn mulid1 nnz gcdid cc0 cle wbr nnre cn0 nnnn0
      cr nn0ge0 absid syl2anc 3eqtrd wa adantr zmulcl syl2an mp3an1 ax1cn adddi
      gcdaddm mp3an3 ax-1cn axmulcom mpan2 eqtrd eqtr4d biimpd expcom a2d nnind
      1z impcom ) BEFAEFZAABGHZIHZAJZVTAACUAZGHZIHZAJZKVTAARGHZIHZAJZKVTAADUAZG
      HZIHZAJZKVTAAWKRLHZGHZIHZAJZKVTWCKCDBWDRJZWGWJVTWSWFWIAWSWEWHAIWDRAGMNOPC
      DUBZWGWNVTWTWFWMAWTWEWLAIWDWKAGMNOPWDWOJZWGWRVTXAWFWQAXAWEWPAIWDWOAGMNOPW
      DBJZWGWCVTXBWFWBAXBWEWAAIWDBAGMNOPVTWIAAIHZAUCUDZAVTWHAAIVTASFZWHAJAUEZAU
      FQNVTATFZXCXDJAUGZAUHQVTAUOFUIAUJUKZXDAJAULVTAUMFXIAUNAUPQAUQURUSWKEFZVTW
      NWRVTXJWNWRKVTXJUTZWNWRXKWMWQAXKWMAWLRAGHZLHZIHZWQXKXGWLTFZWMXNJZVTXGXJXH
      VAVTXGWKTFXOXJXHWKUGAWKVBVCRTFXGXOXPVRRAWLVGVDURXKWPXMAIVTXEWKSFZWPXMJXJX
      FWKUEXEXQUTZWPWLWHLHZXMXEXQRSFZWPXSJVEAWKRVFVHXRWHXLWLLXEWHXLJZXQXEXTYAVI
      ARVJVKVANVLVCNVMOVNVOVPVQVS $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  ${
    $( Extend ~ gcdmultiple so ` N ` can be an integer.  (Contributed by Scott
       Fenton, 18-Apr-2014.) $)
    gcdmultiplez $p |- ( ( M e. NN /\ N e. ZZ ) ->
       ( M gcd ( M x. N ) ) = M ) $=
      ( cn wcel cz wa cmul co cgcd wceq cc0 oveq2 oveq2d cabs cfv cc syl adantr
      syl2anc eqtrd eqeq1d wne nncn zcn absmul syl2an cr cle wbr nnre cn0 nnnn0
      nn0ge0 absid oveq1d simpll nn0z 3syl nnz zmulcl sylan gcdabs2 gcdmultiple
      nnabscl sylan2 anassrs 3eqtr3d mul01 nn0gcdid0 pm2.61ne ) ACDZBEDZFZAABGH
      ZIHZAJAAKGHZIHZAJBKBKJZVOVQAVRVNVPAIBKAGLMUAVMBKUBZFZAVNNOZIHZAABNOZGHZIH
      ZVOAVMWBWEJVSVMWAWDAIVMWAANOZWCGHZWDVKAPDZBPDWAWGJVLAUCZBUDABUEUFVKWGWDJV
      LVKWFAWCGVKAUGDKAUHUIZWFAJAUJVKAUKDZWJAULZAUMQAUNSUORTMRVTAEDZVNEDZWBVOJV
      TVKWKWMVKVLVSUPWLAUQURVMWNVSVKWMVLWNAUSABUTVARVNAVBSVKVLVSWEAJZVLVSFVKWCC
      DWOBVDAWCVCVEVFVGVMVQAKIHZAVKVQWPJZVLVKWHWQWIWHVPKAIAVHMQRVKWPAJZVLVKWKWR
      WLAVIQRTVJ $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  $( ` A ` is equal to its gcd with ` B ` if and only if ` A ` divides ` B ` .
     (Contributed by Mario Carneiro, 23-Feb-2014.) $)
  gcdeq $p |- ( ( A e. NN /\ B e. NN ) -> ( ( A gcd B ) = A <-> A || B ) ) $=
    ( cn wcel wa cgcd wceq cdivides wbr cz nnz syl2an cle adantr syl wi syl2anc
    cc0 simpl cr co gcddvds simprd breq1 syl5ibcom iddvds adantl nnne0 necon3ai
    wn wne dvdslegcd syl31anc mpand simpld cn0 gcdcl dvdsle mpd jctild wb nn0re
    nn0z nnre letri3 sylibrd impbid ) ACDZBCDZEZABFUAZAGZABHIZVJVKBHIZVLVMVJVKA
    HIZVNVHAJDZBJDZVOVNEVIAKZBKZABUBLZUCVKABHUDUEVJVMVKAMIZAVKMIZEZVLVJVMWBWAVJ
    AAHIZVMWBVJVPWDVHVPVIVRNZAUFOVJVPVPVQARGZBRGZEZUJZWDVMEWBPWEWEVIVQVHVSUGVHW
    IVIVHARUKWIAUHWHARWFWGSUIONAABULUMUNVJVOWAVJVOVNVTUOVJVKJDZVHVOWAPVJVKUPDZW
    JVHVPVQWKVIVRVSABUQLZVKVCOVHVISVKAURQUSUTVJVKTDZATDZVLWCVAVJWKWMWLVKVBOVHWN
    VIAVDNVKAVEQVFVG $.
    $( [23-Feb-2014] $)

  ${
    $d M k $.  $d N k $.
    $( Unidirectional form of ~ dvdssq .  (Contributed by Scott Fenton,
       19-Apr-2014.) $)
    dvdssqim $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
       ( M || N -> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( vk cz wcel wa cdivides wbr cv cmul co wceq wrex c2 cexp divides syl2anr
      zsqcl cc zcn wi dvdsmul2 sqmul breqtrrd breq2d syl5ibcom rexlimdva adantr
      oveq1 sylbid ) ADEZBDEZFABGHCIZAJKZBLZCDMZANOKZBNOKZGHZCABPUKUPUSUAULUKUO
      USCDUKUMDEZFZUQUNNOKZGHUOUSVAUQUMNOKZUQJKZVBGUTVCDEUQDEUQVDGHUKUMRARVCUQU
      BQUTUMSEASEVBVDLUKUMTATUMAUCQUDUOVBURUQGUNBNOUIUEUFUGUHUJ $.
      $( [19-Apr-2014] $) $( [19-Apr-2014] $)
  $}

  ${
    $( If ` K ` and ` M ` are relatively prime, then the GCD of ` K ` and
       ` M x. N ` is the GCD of ` K ` and ` N ` .  (Contributed by Scott
       Fenton, 12-Apr-2014.) $)
    rprimemulgcd $p |- ( ( ( K e. NN /\ M e. NN /\ N e. NN ) /\
                  ( K gcd M ) = 1 ) -> ( K gcd ( M x. N ) ) = ( K gcd N ) ) $=
      ( cn wcel w3a cgcd co c1 wceq wa gcdmultiple 3adant2 cz nnz zmulcl syl2an
      cmul adantr eqtrd oveq1d 3ad2ant1 3adant1 gcdass syl3anc eqtr3d cn0 nnnn0
      mulgcdr syl3an oveq1 sylan9eq cc nncn 3ad2ant3 mulid2 syl oveq2d ) ADEZBD
      EZCDEZFZABGHZIJZKZABCRHZGHZAACRHZVFGHZGHZACGHVBVGVJJVDVBAVHGHZVFGHZVGVJVB
      VKAVFGUSVAVKAJUTACLMUAVBANEZVHNEZVFNEZVLVJJUSUTVMVAAOZUBUSVAVNUTUSVMCNEZV
      NVAVPCOZACPQMUTVAVOUSUTBNEZVQVOVABOZVRBCPQUCVFVHAUDUEUFSVEVICAGVEVIICRHZC
      VBVDVIVCCRHZWAUSVMUTVSVACUGEVIWBJVPVTCUHABCUIUJVCICRUKULVECUMEZWACJVBWCVD
      VAUSWCUTCUNUOSCUPUQTURT $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  ${
    $d A n k $.  $d B n k $.  $d N n k $.
    $( If ` A ` and ` B ` are relatively prime, then so are ` A ^ N ` and
       ` B ` .  (Contributed by Scott Fenton, 12-Apr-2014.) $)
    rprimelpwr $p |- ( ( A e. NN /\ B e. NN /\ N e. NN ) ->
       ( ( A gcd B ) = 1 -> ( ( A ^ N ) gcd B ) = 1 ) ) $=
      ( cn wcel cgcd co c1 wceq wi wa oveq2 oveq1d eqeq1d imbi2d adantr syl2anc
      cexp syl cz vk vn cv caddc weq cc nncn exp1 biimpar w3a df-3an cn0 simpl1
      cmul simpl3 nnnn0 expp1 simp1 3ad2ant3 nnexpcl mulcom eqtrd oveq2d simpl2
      nnz 3ad2ant1 3ad2ant2 gcdcom biimpa rprimemulgcd syl31anc 3eqtr4d biimprd
      zcn peano2nn sylanbr an32s expcom a2d nnind exp3a com12 3impia ) ADEZBDEZ
      CDEZABFGZHIZACRGZBFGZHIZJZWFWDWEKZWLWFWMWHWKWMWHKZAUAUCZRGZBFGZHIZJWNAHRG
      ZBFGZHIZJWNAUBUCZRGZBFGZHIZJWNAXBHUDGZRGZBFGZHIZJWNWKJUAUBCWOHIZWRXAWNXJW
      QWTHXJWPWSBFWOHARLMNOUAUBUEZWRXEWNXKWQXDHXKWPXCBFWOXBARLMNOWOXFIZWRXIWNXL
      WQXHHXLWPXGBFWOXFARLMNOWOCIZWRWKWNXMWQWJHXMWPWIBFWOCARLMNOWMXAWHWMWTWGHWD
      WTWGIWEWDWSABFWDAUFEZWSAIAUGZAUHSMPNUIXBDEZWNXEXIWNXPXEXIJZWMXPWHXQWMXPKW
      DWEXPUJZWHXQWDWEXPUKXRWHKZXIXEXSXHXDHXSBXGFGZBXCFGZXHXDXSXTBAXCUNGZFGZYAX
      SXGYBBFXSXGXCAUNGZYBXSXNXBULEZXGYDIXSWDXNWDWEXPWHUMZXOSZXSXPYEWDWEXPWHUOX
      BUPZSAXBUQQXSXCUFEZXNYDYBIXSXCTEZYIXRYJWHXRXCDEZYJXRWDYEYKWDWEXPURXPWDYEW
      EYHUSAXBUTQZXCVESPZXCVNSYGXCAVAQVBVCXSWEWDYKBAFGZHIZYCYAIWDWEXPWHVDYFXRYK
      WHYLPXRWHYOXRWGYNHXRATEZBTEZWGYNIWDWEYPXPAVEVFWEWDYQXPBVEVGZABVHQNVIBAXCV
      JVKVBXSXGTEZYQXHXTIXSXGDEZYSXSWDXFULEZYTYFXSXFDEZUUAXRUUBWHXPWDUUBWEXBVOU
      SPXFUPSAXFUTQXGVESXRYQWHYRPZXGBVHQXSYJYQXDYAIYMUUCXCBVHQVLNVMVPVQVRVSVTWA
      WBWC $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  $( If ` A ` and ` B ` are relatively prime, then so are ` A ^ N ` and
     ` B ^ N ` .  (Contributed by Scott Fenton, 12-Apr-2014.) $)
  rprimepwr $p |- ( ( A e. NN /\ B e. NN /\ N e. NN ) ->
      ( ( A gcd B ) = 1 -> ( ( A ^ N ) gcd ( B ^ N ) ) = 1 ) ) $=
    ( cn wcel w3a cgcd co c1 wceq cexp wa syl syl2anc 3jca cz gcdcom rprimelpwr
    nnz sylc simpl1 simpl2 simpl3 nnnn0 nnexpcl 3ad2ant1 3ad2ant2 eqeq1d biimpa
    cn0 eqtrd ex ) ADEZBDEZCDEZFZABGHZIJZACKHBCKHZGHIJZUPURLZUMUSDEZUOFAUSGHZIJ
    UTVAUMVBUOUMUNUOURUAZVAUNCUJEZVBUMUNUOURUBZVAUOVEUMUNUOURUCZCUDMBCUENZVGOVA
    VCUSAGHZIVAAPEZUSPEZVCVIJVAUMVJVDASZMVAVBVKVHUSSMAUSQNVAUNUMUOFBAGHZIJZVIIJ
    VAUNUMUOVFVDVGOUPURVNUPUQVMIUPVJBPEZUQVMJUMUNVJUOVLUFUNUMVOUOBSUGABQNUHUIBA
    CRTUKAUSCRTUL $.
    $( [19-Apr-2014] $) $( [12-Apr-2014] $)

  ${
    $( Square distributes over GCD. (Contributed by Scott Fenton,
       18-Apr-2014.) $)
    sqgcd $p |- ( ( M e. NN /\ N e. NN ) ->
           ( ( M gcd N ) ^ 2 ) = ( ( M ^ 2 ) gcd ( N ^ 2 ) ) ) $=
      ( cn wcel wa cgcd co c2 cexp c1 cc wceq cz cc0 nnz adantr syl2anc syl wbr
      cdiv cmul wn anim12i wne nnne0 df-ne sylib intnanrd gcdn0cl nnsqcl mulid1
      cdivides adantl gcddvds syl2an simpld dvdssqim mpd simprd gcddiv syl32anc
      nncn wi sqdiv syl3anc oveq12d syl31anc divid eqtr3d clt wb divides2 mpbid
      cr nnre nngt0 divgt0 syl22anc elnnz sylanbrc 2nn rprimepwr mp3an3 3eqtr2d
      ax-1cn divmul mp3an2 syl12anc ) ACDZBCDZEZABFGZHIGZJUAGZWMAHIGZBHIGZFGZWK
      WMKDZWNWMLWKWMCDZWRWKWLCDZWSWKAMDZBMDZEANLZBNLZEUBZWTWIXAWJXBAOZBOZUCWIXE
      WJWIXCXDWIANUDXCUBAUEANUFUGUHPABUIQZWLUJRZWMVBRZWMUKRWKWQWMTGZJLZWNWQLZWK
      XKWOWMTGZWPWMTGZFGZAWLTGZHIGZBWLTGZHIGZFGZJWKWOMDZWPMDZWSWMWOULSZWMWPULSZ
      XKXPLWIYBWJWIWOCDZYBAUJZWOORZPWJYCWIWJWPCDYCBUJWPORZUMXIWKWLAULSZYDWKYJWL
      BULSZWIXAXBYJYKEZWJXFXGABUNUOZUPZWKWLMDZXAYJYDVCWKWTYOXHWLORZWIXAWJXFPZWL
      AUQQURWKYKYEWKYJYKYMUSZWKYOXBYKYEVCYPWJXBWIXGUMZWLBUQQURWOWPWMUTVAWKXRXNX
      TXOFWKAKDZWLKDZWLNUDZXRXNLWIYTWJAVBPWKWTUUAXHWLVBRZWKWTUUBXHWLUERZAWLVDVE
      WKBKDZUUAUUBXTXOLWJUUEWIBVBUMUUCUUDBWLVDVEVFWKXQXSFGZJLZYAJLZWKWLWLTGZUUF
      JWKXAXBWTYLUUIUUFLYQYSXHYMABWLUTVGWKUUAUUBUUIJLUUCUUDWLVHQVIWKXQCDZXSCDZU
      UGUUHVCZWKXQMDZNXQVJSZUUJWKYJUUMYNWKYOUUBXAYJUUMVKYPUUDYQWLAVLVEVMWKAVNDZ
      NAVJSZWLVNDZNWLVJSZUUNWIUUOWJAVOPWIUUPWJAVPPWKWTUUQXHWLVORZWKWTUURXHWLVPR
      ZAWLVQVRXQVSVTWKXSMDZNXSVJSZUUKWKYKUVAYRWKYOUUBXBYKUVAVKYPUUDYSWLBVLVEVMW
      KBVNDZNBVJSZUUQUURUVBWJUVCWIBVOUMWJUVDWIBVPUMUUSUUTBWLVQVRXSVSVTUUJUUKHCD
      UULWAXQXSHWBWCQURWDWKWQKDZWRWMNUDZXLXMVKZWKWQCDZUVEWKYBYCEWONLZWPNLZEUBZU
      VHWIYBWJYCYHYIUCWIUVKWJWIUVIUVJWIWONUDZUVIUBWIYFUVLYGWOUERWONUFUGUHPWOWPU
      IQWQVBRXJWKWSUVFXIWMUERUVEJKDWRUVFEUVGWEWQJWMWFWGWHVMVI $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Lemma for ~ dvdssq .  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    dvdssqlem $p |- ( ( M e. NN /\ N e. NN ) ->
             ( M || N <-> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( cn wcel wa cdivides wbr c2 cexp co cz syl2an cgcd wceq adantr wb nnsqcl
      nnz cr syl wi dvdssqim sqgcd gcdeq biimpar eqtrd cc0 cle cn0 gcdcl nn0ge0
      nn0re nnre nnnn0 sq11 syl22anc mpbid gcddvds simprd eqbrtrrd ex impbid )
      ACDZBCDZEZABFGZAHIJZBHIJZFGZVCAKDZBKDZVFVIUAVDARZBRZABUBLVEVIVFVEVIEZABMJ
      ZABFVNVOHIJZVGNZVOANZVNVPVGVHMJZVGVEVPVSNVIABUCOVEVSVGNZVIVCVGCDVHCDVTVIP
      VDAQBQVGVHUDLUEUFVEVQVRPZVIVEVOSDZUGVOUHGZASDZUGAUHGZWAVEVOUIDZWBVCVJVKWF
      VDVLVMABUJLZVOULTVEWFWCWGVOUKTVCWDVDAUMOVCWEVDVCAUIDWEAUNAUKTOVOAUOUPOUQV
      NVOAFGZVOBFGZVEWHWIEZVIVCVJVKWJVDVLVMABURLOUSUTVAVB $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $( Two numbers are divisible iff their squares are.  (Contributed by Scott
       Fenton, 18-Apr-2014.) $)
    dvdssq $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
             ( M || N <-> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( cz wcel wa cdivides wbr c2 cexp co wb cc0 wceq cabs cfv cn syl2an zsqcl
      syl adantr breq1 breq1d bibi12d wne nnabscl breq2 breq2d dvdssqlem sylan2
      sq0i nnz simpl dvdsabsb nnsqcl cc zcn abssq adantl bitr4d 3bitr4d anassrs
      dvds0 2thd pm2.61ne sylan absdvdsb adantlr eqcomd bitrd an32s sqeq0 0dvds
      3bitr4rd ) ACDZBCDZEABFGZAHIJZBHIJZFGZKZLBFGZLVRFGZKZALALMZVPWAVSWBALBFUA
      WDVQLVRFAUJUBUCVNALUDZVOVTVNWEEZVOEZANOZBFGZWHHIJZVRFGZVPVSWFWHPDZVOWIWKK
      ZAUEWLVOEWMWHLFGZWJLFGZKZBLBLMZWIWNWKWOBLWHFUFWQVRLWJFBUJUGUCWLVOBLUDZWMW
      LVOWREZEZWHBNOZFGZWJXAHIJZFGZWIWKWSWLXAPDXBXDKBUEWHXAUHUIWLWHCDZVOWIXBKWS
      WHUKZVOWRULWHBUMQWTWKWJVRNOZFGZXDWLWJCDZVRCDZWKXHKWSWLWJPDXIWHUNWJUKSVOXJ
      WRBRZTWJVRUMQWSXDXHKWLWSXCXGWJFWSBUODZXCXGMVOXLWRBUPZTBUQSUGURUSUTVAWLWPV
      OWLXEWPXFXEWNWOWHVBXEXIWOWHRWJVBSVCSTVDVEVNVOVPWIKWEABVFVGWGVSVQNOZVRFGZW
      KWFVQCDZXJVSXOKVOVNXPWEARTXKVQVRVFQWFXOWKKVOWFXNWJVRFVNXNWJMWEVNWJXNVNAUO
      DWJXNMAUPAUQSVHTUBTVIUTVJVOWCVNVOVRLMZWQWBWAVOXLXQWQKXMBVKSVOXJWBXQKXKVRV
      LSBVLVMURVD $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

