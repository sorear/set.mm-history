$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             The divides relation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c || $.

  $( Extend the definition of a class to include the divides relation. $)
  cdivides $a class || $.

  ${
    $d n x y $.
    $( Define the divides relation. $)
    df-divides $a |- || = { <. x , y >. | ( ( x e. ZZ /\ y e. ZZ ) /\
                                            E. n e. ZZ ( n x. x ) = y ) } $.
  $}

  ${
    $d M n x y $.  $d N n x y $.
    $( Divides relation. ` M || N ` means ` M ` divides into ` N ` with no
       remainder.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    divides $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                    ( M || N <-> E. n e. ZZ ( n x. M ) = N ) ) $=
      ( vx vy cdivides wbr cop cv cz wcel wa cmul co wceq wrex copab df-divides
      df-br rexbidv eleq2i bitri oveq2 eqeq1d eqeq2 opelopab2 syl5bb ) BCFGZBCH
      ZDIZJKEIZJKLAIZUJMNZUKOZAJPZLDEQZKZBJKCJKLULBMNZCOZAJPZUHUIFKUQBCFSFUPUID
      EARUAUBUOURUKOZAJPUTDEBCJJUJBOZUNVAAJVBUMURUKUJBULMUCUDTUKCOVAUSAJUKCURUE
      TUFUG $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d k M $.  $d k N $.
    $( One nonzero integer divides another integer if and only if their
       quotient is an integer.  (Contributed by Jeff Hankins, 29-Sep-2013.) $)
    divides2 $p |- ( ( M e. ZZ /\ M =/= 0 /\ N e. ZZ ) -> ( M || N <-> ( N / M
    ) e. ZZ ) ) $=
      ( vk cz wcel cc0 wne w3a cdivides wbr cv cmul co wceq wb wa cc zcn adantr
      wrex cdiv divides 3adant2 3ad2ant3 adantl 3ad2ant1 simpl2 syl112anc eqcom
      divmul3 syl6bb biimprd impr simprl eqeltrd exp32 rexlimdv divcan1 syl3anc
      simpr simp2 oveq1 eqeq1d rcla4ev syl2anc ex impbid bitrd ) ADEZAFGZBDEZHZ
      ABIJZCKZALMZBNZCDTZBAUAMZDEZVIVKVMVQOVJCABUBUCVLVQVSVLVPVSCDVLVNDEZVPVSVL
      VTVPPPVRVNDVLVTVPVRVNNZVLVTPZWAVPWBWABVONZVPWBBQEZVNQEZAQEZVJWAWCOVLWDVTV
      KVIWDVJBRUDZSVTWEVLVNRUEVLWFVTVIVJWFVKARUFZSVIVJVKVTUGBVNAUJUHBVOUIUKULUM
      VLVTVPUNUOUPUQVLVSVQVLVSPVSVRALMZBNZVQVLVSUTVLWJVSVLWDWFVJWJWGWHVIVJVKVAB
      AURUSSVPWJCVRDVNVRNVOWIBVNVRALVBVCVDVEVFVGVH $.
      $( [29-Sep-2013] $)
  $}

  ${
    $d k M $.  $d k N $.
    $( One nonzero integer divides another integer if and only if the remainder
       upon division is zero.  (Contributed by Mario Carneiro, 22-Feb-2014.) $)
    divides3 $p |- ( ( M e. NN /\ N e. ZZ ) ->
                     ( M || N <-> ( N mod M ) = 0 ) ) $=
      ( cn wcel cz wa cdivides wbr cdiv co cmo cc0 wceq wne wb nnz jca divides2
      nnne0 3expa sylan cr crp zre nnrp mod0 syl2anr bitr4d ) ACDZBEDZFABGHZBAI
      JEDZBAKJLMZUIAEDZALNZFUJUKULOZUIUNUOAPASQUNUOUJUPABRTUAUJBUBDAUCDUMULOUIB
      UDAUEBAUFUGUH $.
      $( [15-Jul-2014] $) $( [22-Feb-2014] $)
  $}

  $( Two ways to say ` A == B ( mod N ) ` . $)
  moddvds $p |- ( ( N e. NN /\ A e. ZZ /\ B e. ZZ ) ->
                  ( ( A mod N ) = ( B mod N ) <-> N || ( A - B ) ) ) $=
    ( wcel cz cmo co wceq wb wa cc0 syl caddc zre modadd1 3expia syl2anc oveq1d
    cr wi cn cmin cdivides wbr crp nnrp adantr eqeq2d ad2antrl ad2antll renegcl
    0mod cneg syl22anc recnd negsub negid eqeq12d sylibd resubcl 0re a1i addid2
    cc npcan impbid zsubcl divides3 sylan2 3bitr4d 3impb ) CUADZAEDZBEDZACFGZBC
    FGZHZCABUBGZUCUDZIVLVMVNJZJZVRCFGZKCFGZHZWBKHZVQVSWAWCKWBWACUEDZWCKHVLWFVTC
    UFUGZCULLUHWAVQWDWAVQABUMZMGZCFGZBWHMGZCFGZHZWDWAASDZBSDZWHSDZWFVQWMTVMWNVL
    VNANUIZVNWOVLVMBNUJZWAWOWPWRBUKLWGWNWOJWPWFJVQWMABWHCOPUNWAWJWBWLWCWAWIVRCF
    WAAVDDZBVDDZWIVRHWAAWQUOZWABWRUOZABUPQRWAWKKCFWAWTWKKHXBBUQLRURUSWAWDVRBMGZ
    CFGZKBMGZCFGZHZVQWAVRSDZKSDZWOWFWDXGTWAWNWOXHWQWRABUTQXIWAVAVBWRWGXHXIJWOWF
    JWDXGVRKBCOPUNWAXDVOXFVPWAXCACFWAWSWTXCAHXAXBABVEQRWAXEBCFWAWTXEBHXBBVCLRUR
    USVFVTVLVREDVSWEIABVGCVRVHVIVJVK $.
    $( [18-Feb-2014] $)

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( A lemma to assist theorems of ` || ` with no antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds0lem $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ ( K x. M ) = N ) ->
                     M || N ) $=
      ( vx cz wcel w3a cmul co wceq cdivides wi wa cv wrex oveq1 eqeq1d rcla4ev
      wbr adantl wb divides adantr mpbird expr 3impa 3comr imp ) AEFZBEFZCEFZGA
      BHIZCJZBCKSZUJUKUIUMUNLZUJUKUIUOUJUKMZUIUMUNUPUIUMMZMUNDNZBHIZCJZDEOZUQVA
      UPUTUMDAEURAJUSULCURABHPQRTUPUNVAUAUQDBCUBUCUDUEUFUGUH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d J x $.  $d K x $.  $d M x z $.  $d N x z $.  $d Z z $.  $d ph x $.
    dvds1lem.1 $e |- ( ph -> ( J e. ZZ /\ K e. ZZ ) ) $.
    dvds1lem.2 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds1lem.3 $e |- ( ( ph /\ x e. ZZ ) -> Z e. ZZ ) $.
    dvds1lem.4 $e |- ( ( ph /\ x e. ZZ ) ->
                       ( ( x x. J ) = K -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with one antecedent.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds1lem $p |- ( ph -> ( J || K -> M || N ) ) $=
      ( vz cv cmul co wceq cz wrex wcel wa cdivides wbr oveq1 eqeq1d rcla4ev ex
      sylan syld rexlimdva wb divides syl 3imtr4d ) ABMZCNODPZBQRZLMZENOZFPZLQR
      ZCDUAUBZEFUAUBZAUOUTBQAUNQSTZUOGENOZFPZUTKVCVEUTVCGQSVEUTJUSVELGQUQGPURVD
      FUQGENUCUDUEUGUFUHUIACQSDQSTVAUPUJHBCDUKULAEQSFQSTVBUTUJILEFUKULUM $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d L x y $.  $d M x y z $.
    $d N x y z $.  $d Z z $.  $d ph x y $.
    dvds2lem.1 $e |- ( ph -> ( I e. ZZ /\ J e. ZZ ) ) $.
    dvds2lem.2 $e |- ( ph -> ( K e. ZZ /\ L e. ZZ ) ) $.
    dvds2lem.3 $e |- ( ph -> ( M e. ZZ /\ N e. ZZ ) ) $.
    dvds2lem.4 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) -> Z e. ZZ ) $.
    dvds2lem.5 $e |- ( ( ph /\ ( x e. ZZ /\ y e. ZZ ) ) ->
               ( ( ( x x. I ) = J /\ ( y x. K ) = L ) -> ( Z x. M ) = N ) ) $.
    $( A lemma to assist theorems of ` || ` with two antecedents.  (Contributed
       by Paul Chapman, 21-Mar-2011.) $)
    dvds2lem $p |- ( ph -> ( ( I || J /\ K || L ) -> M || N ) ) $=
      ( wa cmul wceq cz wcel vz cdivides cv co wrex wb divides bi2anan9 syl2anc
      wbr biimpd reeanv syl6ibr oveq1 eqeq1d rcla4ev ex syld rexlimdvva sylibrd
      sylan syl ) ADEUBUJZFGUBUJZPZUAUCZHQUDZIRZUASUEZHIUBUJZAVEBUCZDQUDERZCUCZ
      FQUDGRZPZCSUEBSUEZVIAVEVLBSUEZVNCSUEZPZVPAVEVSADSTESTPZFSTGSTPZVEVSUFKLVT
      VCVQWAVDVRBDEUGCFGUGUHUIUKVLVNBCSSULUMAVOVIBCSSAVKSTVMSTPPZVOJHQUDZIRZVIO
      WBWDVIWBJSTWDVINVHWDUAJSVFJRVGWCIVFJHQUNUOUPVAUQURUSURAHSTISTPVJVIUFMUAHI
      UGVBUT $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides itself.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  iddvds $p |- ( N e. ZZ -> N || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn mulid2 syl 1z mp3anl1 anabsan
    dvds0lem mpdan ) ABCZDAEFAGZAAHIZSAJCTAKALMSTUADBCSSTUANDAAQOPR $.
    $( [21-Mar-2011] $)

  $( 1 divides any integer.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  1dvds $p |- ( N e. ZZ -> 1 || N ) $=
    ( cz wcel c1 cmul co wceq cdivides wbr cc zcn mulid1 syl 1z mp3anl2 anabsan
    dvds0lem mpdan ) ABCZADEFAGZDAHIZSAJCTAKALMSTUASDBCSTUANADAQOPR $.
    $( [21-Mar-2011] $)

  $( Any integer divides 0.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvds0 $p |- ( N e. ZZ -> N || 0 ) $=
    ( cz wcel cc0 cmul co wceq cdivides wbr cc zcn mul02 syl wi 0z w3a dvds0lem
    ex mp3an13 mpd ) ABCZDAEFDGZADHIZUAAJCUBAKALMDBCZUAUDUBUCNOOUDUAUDPUBUCDADQ
    RST $.
    $( [21-Mar-2011] $)

  ${
    $d M x $.  $d N x $.
    $( An integer divides another iff its negation does.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    negdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> -u M || N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg znegcl cmul co wceq zcn syl2anr adantlr
      cc eqeq1d biimprd dvds1lem cv id anim1i adantl mul2neg mulneg12 impbid )
      ADEZBDEZFZABGHAIZBGHUJCABUKBCUAZIZUJUBZUHUKDEUIAJUCZULDEZUMDEUJULJUDZUJUP
      FZUMUKKLZBMULAKLZBMURUSUTBUHUPUSUTMZUIUPULQEZAQEZVAUHULNZANZULAUEOPRSTUJC
      UKBABUMUOUNUQURUMAKLZBMULUKKLZBMURVFVGBUHUPVFVGMZUIUPVBVCVHUHVDVEULAUFOPR
      STUG $.
      $( [21-Mar-2011] $)

    $( An integer divides another iff it divides its negation.  (Contributed by
       Paul Chapman, 21-Mar-2011.) $)
    dvdsnegb $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || -u N ) ) $=
      ( vx cz wcel wa cdivides wbr cneg cv id znegcl cmul co wi cc zcn dvds1lem
      wceq negeq anim2i adantl mulneg1 eqeq2d syl5ibcom syl2anr negneg sylan9eq
      adantlr sylan9eqr expr 3impa syl3an 3coml 3expa impbid ) ADEZBDEZFZABGHAB
      IZGHUSCABAUTCJZIZUSKZURUTDEUQBLUAZVADEZVBDEUSVALUBZUQVEVAAMNZBSZVBAMNZUTS
      ZOZURVEVAPEZAPEZVKUQVAQZAQZVLVMFZVIVGIZSVHVJVAAUCZVHVQUTVIVGBTUDUEUFUIRUS
      CAUTABVBVDVCVFUQURVEVGUTSZVIBSZOZVEUQURWAVEVLUQVMURBPEZWAVNVOBQVLVMWBWAVP
      WBVSVTVPWBVSFVIVQBVRVSWBVQUTIBVGUTTBUGUJUHUKULUMUNUORUP $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides another iff its absolute value does.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  absdvdsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> ( abs ` M ) || N ) ) $=
    ( cz wcel wa cabs cfv wceq wo cdivides wbr wb cr zre absor syl adantr breq1
    cneg bicomd wi a1i negdvdsb sylan9bb ex jaod mpd ) ACDZBCDZEZAFGZAHZUKASZHZ
    IZABJKZUKBJKZLZUHUOUIUHAMDUOANAOPQUJULURUNULURUAUJULUQUPUKABJRTUBUJUNURUJUP
    UMBJKZUNUQABUCUNUQUSUKUMBJRTUDUEUFUG $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsabsb $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
                   ( M || N <-> M || ( abs ` N ) ) ) $=
    ( cz wcel wa cabs cfv wceq wo cdivides wbr wb cr zre absor syl adantl breq2
    cneg bicomd wi a1i dvdsnegb sylan9bb ex jaod mpd ) ACDZBCDZEZBFGZBHZUKBSZHZ
    IZABJKZAUKJKZLZUIUOUHUIBMDUOBNBOPQUJULURUNULURUAUJULUQUPUKBAJRTUBUJUNURUJUP
    AUMJKZUNUQABUCUNUQUSUKUMAJRTUDUEUFUG $.
    $( [21-Mar-2011] $)

  ${
    $d N n $.
    $( Only 0 is divisible by 0 .  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    0dvds $p |- ( N e. ZZ -> ( 0 || N <-> N = 0 ) ) $=
      ( vn cz wcel cc0 cdivides wbr wceq cv cmul co wrex wb 0z divides mpan zcn
      cc mul01 syl eqtr2 sylan2 ancoms syl6bi dvds0 ax-mp breq2 mpbiri impbid1
      rexlimiva ) ACDZEAFGZAEHZUKULBIZEJKZAHZBCLZUMECDZUKULUQMNBEAOPUPUMBCUPUNC
      DZUMUSUPUOEHZUMUSUNRDUTUNQUNSTUOAEUAUBUCUJUDUMULEEFGZURVANEUEUFAEEFUGUHUI
      $.
      $( [21-Mar-2011] $)
  $}

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul1 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> M || ( M x. N ) ) $=
    ( cz wcel wa cmul co wceq cdivides wbr cc mulcom syl2anr wi zmulcl dvds0lem
    zcn w3a ex 3com12 mpd3an3 mpd ) ACDZBCDZEBAFGABFGZHZAUEIJZUDBKDAKDUFUCBQAQB
    ALMUCUDUECDZUFUGNZABOUDUCUHUIUDUCUHRUFUGBAUEPSTUAUB $.
    $( [21-Mar-2011] $)

  $( An integer divides a multiple of itself.  (Contributed by Paul Chapman,
     21-Mar-2011.) $)
  dvdsmul2 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> N || ( M x. N ) ) $=
    ( cz wcel cmul co cdivides wbr zmulcl w3a wceq eqid dvds0lem mpan2 mpd3an3
    ) ACDZBCDZABEFZCDZBRGHZABIPQSJRRKTRLABRMNO $.
    $( [21-Mar-2011] $)

  $( An integer divides a positive integer power of itself.  (Contributed by
     Paul Chapman, 26-Oct-2012.) $)
  iddvdsexp $p |- ( ( M e. ZZ /\ N e. NN ) -> M || ( M ^ N ) ) $=
    ( cz wcel cn wa c1 cmin co cexp cmul cdivides wbr cn0 nnm1nn0 zexpcl sylan2
    simpl dvdsmul2 syl2anc cc wceq zcn expm1t sylan breqtrrd ) ACDZBEDZFZAABGHI
    ZJIZAKIZABJIZLUIUKCDZUGAULLMUHUGUJNDUNBOAUJPQUGUHRUKASTUGAUADUHUMULUBAUCABU
    DUEUF $.
    $( [26-Oct-2012] $)

  ${
    $d K x $.  $d M x $.  $d N x $.
    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> K || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl anim1i 3simpb ancoms 3ad2antl2 wceq
      3impa cc zcn mulass mul32 eqtr3d syl3an 3adantl3 eqeq1d biimpd dvds1lem
      3coml 3expa ) AEFZBEFZCEFZGZDABHIZCACDJZBHIZUKULUMUOEFZUMKUKULKURUMABLMRU
      KULUMNULUKUPEFZUQEFZUMUSULUTUPBLOPUNUSKZUPUOHIZCQUQAHIZCQVAVBVCCUKULUSVBV
      CQZUMUKULUSVDUSUKULVDUSUPSFZUKASFZULBSFZVDUPTATBTVEVFVGGUPAHIBHIVBVCUPABU
      AUPABUBUCUDUIUJUEUFUGUH $.
      $( [21-Mar-2011] $)

    $( If a product divides an integer, so does one of its factors.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    muldvds2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K x. M ) || N -> M || N ) ) $=
      ( vx cz wcel w3a cmul co cv wa zmulcl anim1i 3simpc ancoms 3ad2antl1 wceq
      3impa cc zcn mulass syl3an 3coml 3expa 3adantl3 eqeq1d biimprd dvds1lem )
      AEFZBEFZCEFZGZDABHIZCBCDJZAHIZUIUJUKUMEFZUKKUIUJKUPUKABLMRUIUJUKNUIUJUNEF
      ZUOEFZUKUQUIURUNALOPULUQKZUOBHIZCQUNUMHIZCQUSUTVACUIUJUQUTVAQZUKUIUJUQVBU
      QUIUJVBUQUNSFUIASFUJBSFVBUNTATBTUNABUAUBUCUDUEUFUGUH $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdscmul $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( K x. M ) || ( K x. N ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc zmulcl 3adant3 wceq cc
      zcn 3coml 3adant2 jca simpr wa mul12 syl3an 3expa 3adantl3 oveq2 sylan9eq
      ex dvds1lem ) AEFZBEFZCEFZBCGHABIJZACIJZGHKUMUNUOLZDBCUPUQDMZUMUNUONURUPE
      FZUQEFZUMUNUTUOABOPUMUOVAUNACOUAUBURUSEFZUCURVBUDZUSBIJZCQZUSUPIJZUQQVCVE
      VFAVDIJZUQUMUNVBVFVGQZUOUMUNVBVHVBUMUNVHVBUSRFUMARFUNBRFVHUSSASBSUSABUEUF
      TUGUHVDCAIUIUJUKULT $.
      $( [21-Mar-2011] $)

    $( Multiplication by a constant maintains the divides relation.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsmulc $p |- ( ( M e. ZZ /\ N e. ZZ /\ K e. ZZ ) ->
                     ( M || N -> ( M x. K ) || ( N x. K ) ) ) $=
      ( vx cz wcel cdivides wbr cmul co wi w3a cv 3simpc wa zmulcl 3adant2 wceq
      cc zcn 3adant1 jca 3comr simpr mulass syl3an 3com13 3expa oveq1 sylan9req
      3adantl3 ex dvds1lem 3coml ) AEFZBEFZCEFZBCGHBAIJZCAIJZGHKUOUPUQLZDBCURUS
      DMZUOUPUQNUPUQUOUREFZUSEFZOUPUQUOLVBVCUPUOVBUQBAPQUQUOVCUPCAPUAUBUCUTVAEF
      ZUDUTVDOZVABIJZCRZVAURIJZUSRVEVGVHVFAIJZUSUOUPVDVIVHRZUQUOUPVDVJVDUPUOVJV
      DVASFUPBSFUOASFVJVATBTATVABAUEUFUGUHUKVFCAIUIUJULUMUN $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdscmulr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( K x. M ) || ( K x. N ) <-> M || N ) ) $=
      ( vx cz wcel wa w3a cmul co cdivides wbr zmulcl 3adant2 3coml 3adant3r wb
      wceq cc zcn cc0 wne cv 3adant3 jca 3simpa simpr wi anim12i mul12 3adant1r
      anim1i 3expb ancoms eqeq1d mulcl mulcan syl3an1 bitr3d syl3an 3impa 3impb
      3expia imp biimpd dvds1lem dvdscmul impbid ) BEFZCEFZAEFZAUAUBZGZHZABIJZA
      CIJZKLZBCKLZVNDVOVPBCDUCZVIVJVKVOEFZVPEFZGZVLVKVIVJWBVKVIVJHVTWAVKVIVTVJA
      BMUDVKVJWAVIACMNUEOPVIVJVMUFVNVSEFZUGVNWCGVSVOIJZVPRZVSBIJZCRZVNWCWEWGQZV
      IVJVMWCWHUHVIVJVMGZWCWHWCVIWIWHWCVIWIWHWCVIGZVJVMWHWJVSSFZBSFZGZVJCSFZVMA
      SFZVLGZWHWCWKVIWLVSTBTUICTVKWOVLATULWMWNWPHZAWFIJZVPRZWEWGWQWRWDVPWMWPWRW
      DRZWNWPWMWTWPWKWLWTWOWKWLWTVLAVSBUJUKUMUNNUOWMWFSFWNWPWSWGQVSBUPWFCAUQURU
      SUTUMVAOVCVBVDVEVFVIVJVKVRVQUHVLABCVGPVH $.
      $( [21-Mar-2011] $)

    $( Cancellation law for the divides relation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    dvdsmulcr $p |- ( ( M e. ZZ /\ N e. ZZ /\ ( K e. ZZ /\ K =/= 0 ) ) ->
                      ( ( M x. K ) || ( N x. K ) <-> M || N ) ) $=
      ( vx cz wcel wa w3a cmul co cdivides wbr zmulcl 3adant2 3adant3r wb wi cc
      wceq zcn cc0 wne cv 3adant1 jca 3simpa simpr anim12i anim1i 3expa adantrr
      mulass eqeq1d mulcl mulcan2 syl3an1 bitr3d syl3an 3expb 3impa 3coml 3impb
      3expia imp biimpd dvds1lem dvdsmulc impbid ) BEFZCEFZAEFZAUAUBZGZHZBAIJZC
      AIJZKLZBCKLZVNDVOVPBCDUCZVIVJVKVOEFZVPEFZGVLVIVJVKHVTWAVIVKVTVJBAMNVJVKWA
      VICAMUDUEOVIVJVMUFVNVSEFZUGVNWBGVSVOIJZVPSZVSBIJZCSZVNWBWDWFPZVIVJVMWBWGQ
      VIVJVMGZWBWGWBVIWHWGWBVIWHWGWBVIGZVJVMWGWIVSRFZBRFZGZVJCRFZVMARFZVLGZWGWB
      WJVIWKVSTBTUHCTVKWNVLATUIWLWMWOHZWEAIJZVPSZWDWFWPWQWCVPWLWOWQWCSZWMWLWNWS
      VLWJWKWNWSVSBAULUJUKNUMWLWERFWMWOWRWFPVSBUNWECAUOUPUQURUSUTVAVCVBVDVEVFVI
      VJVKVRVQQVLABCVGOVH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d I x y $.  $d J x y $.  $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides any linear
       combination of them.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2ln $p |- ( ( ( I e. ZZ /\ J e. ZZ ) /\
                      ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ) ->
        ( ( K || M /\ K || N ) -> K || ( ( I x. M ) + ( J x. N ) ) ) ) $=
      ( cz wcel wa cmul co caddc jca zmulcl syl2anc adantr syl wceq zcn adantl
      cc vx vy w3a cv simpr1 simpr2 simpr3 simpll simplr zaddcl wi anim12i an4s
      expcom imp adddir 3expa ad3antrrr mul32 syl3anc mulcl mulcom 3eqtrd oveq2
      oveq12d oveqan12d sylan9eq ex dvds2lem ) AFGZBFGZHZCFGZDFGZEFGZUCZHZUAUBC
      DCECADIJZBEIJZKJZUAUDZAIJZUBUDZBIJZKJZVQVMVNVLVMVNVOUEZVLVMVNVOUFZLVQVMVO
      WFVLVMVNVOUGZLVQVMVTFGZWFVQVRFGZVSFGZWIVQVJVNWJVJVKVPUHWGADMNVQVKVOWKVJVK
      VPUIZWHBEMNVRVSUJNLVQWAFGZWCFGZHZHZWBFGZWDFGZHZWEFGVQWOWSVLWOWSUKVPWOVLWS
      WMVJWNVKWSWMVJHWQWNVKHWRWAAMWCBMULUMUNOUOZWBWDUJPWPWACIJZDQZWCCIJZEQZHZWE
      CIJZVTQWPXEXFAXAIJZBXCIJZKJZVTWPXFWBCIJZWDCIJZKJZXAAIJZXCBIJZKJXIWPWBTGZW
      DTGZHZCTGZXFXLQZWPWSXQWTWQXOWRXPWBRWDRULPVQXRWOVQVMXRWFCRPOZXOXPXRXSWBWDC
      UPUQNWPXJXMXKXNKWPWATGZATGZXRXJXMQWOYAVQWMYAWNWAROSZVJYBVKVPWOARURZXTWAAC
      USUTWPWCTGZBTGZXRXKXNQWOYEVQWNYEWMWCRSSZVQYFWOVQVKYFWLBRPOZXTWCBCUSUTVEWP
      XMXGXNXHKWPXATGZYBXMXGQWPYAXRYIYCXTWACVANYDXAAVBNWPXCTGZYFXNXHQWPYEXRYJYG
      XTWCCVANYHXCBVBNVEVCXBXDXGVRXHVSKXADAIVDXCEBIVDVFVGVHVI $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( If an integer divides each of two other integers, it divides their sum.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2add $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M + N ) ) ) $=
      ( vx vy cz wcel w3a caddc co cv 3simpa 3simpb zaddcl anim2i cmul wceq zcn
      wa cc 3impb adantl wi adddir syl3an 3comr 3expb oveq12 sylan9eq 3ad2antl1
      ex dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUMUNUOLUMUNUOMUMUNUOUMUQ
      FGZSUNUOSVAUMBCNOUAURFGZUSFGZSZUTFGUPURUSNUBUMUNVDURAPJZBQUSAPJZCQSZUTAPJ
      ZUQQZUCUOUMVDSZVGVIVJVGVHVEVFIJZUQUMVBVCVHVKQZVBVCUMVLVBURTGVCUSTGUMATGVL
      URRUSRARURUSAUDUEUFUGVEBVFCIUHUIUKUJUL $.
      $( [21-Mar-2011] $)

    $( If an integer divides each of two other integers, it divides their
       difference.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvds2sub $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M - N ) ) ) $=
      ( vx vy cz wcel w3a cmin co cv 3simpa 3simpb wa zsubcl anim2i cmul cc zcn
      wceq 3impb adantl wi subdir syl3an 3comr 3expb oveq12 sylan9eq 3ad2antl1
      ex dvds2lem ) AFGZBFGZCFGZHZDEABACABCIJZDKZEKZIJZUMUNUOLUMUNUOMUMUNUOUMUQ
      FGZNUNUONVAUMBCOPUAURFGZUSFGZNZUTFGUPURUSOUBUMUNVDURAQJZBTUSAQJZCTNZUTAQJ
      ZUQTZUCUOUMVDNZVGVIVJVGVHVEVFIJZUQUMVBVCVHVKTZVBVCUMVLVBURRGVCUSRGUMARGVL
      URSUSSASURUSAUDUEUFUGVEBVFCIUHUIUKUJUL $.
      $( [21-Mar-2011] $)

    $( The divides relation is transitive.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    dvdstr $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                   ( ( K || M /\ M || N ) -> K || N ) ) $=
      ( vx vy cz wcel w3a cv cmul co 3simpa 3simpc 3simpb wa zmulcl adantl wceq
      cc zcn oveq2 adantr eqeq2 mpbid mulass mul12 eqtrd syl3an 3comr 3ad2antl1
      wb 3expb eqeq1d syl5ibr dvds2lem ) AFGZBFGZCFGZHZDEABBCACDIZEIZJKZUPUQURL
      UPUQURMUPUQURNUTFGZVAFGZOZVBFGUSUTVAPQUTAJKZBRZVABJKZCRZOZVBAJKZCRUSVEOZV
      AVFJKZCRZVJVMVHRZVNVGVOVIVFBVAJUAUBVIVOVNUKVGVHCVMUCQUDVLVKVMCUPUQVEVKVMR
      ZURUPVCVDVPVCVDUPVPVCUTSGZVDVASGZUPASGZVPUTTVATATVQVRVSHVKUTVAAJKJKVMUTVA
      AUEUTVAAUFUGUHUIULUJUMUNUO $.
      $( [21-Mar-2011] $)
  $}

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr1 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || M -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr co dvdsmul1 3adant1 wa wi zmulcl dvdstr syld3an3
    cmul mpan2d ) ADEZBDEZCDEZFABGHZBBCQIZGHZAUCGHZTUAUDSBCJKSTUAUCDEZUBUDLUEMT
    UAUFSBCNKABUCOPR $.
    $( [17-Nov-2012] $)

  $( If an integer divides another, it divides a multiple of it.  (Contributed
     by Paul Chapman, 17-Nov-2012.) $)
  dvdsmultr2 $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( K || N -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co wa wb dvdsmul2 biantrud 3adant1 wi simp1
    simp3 zmulcl dvdstr syl3anc sylbid ) ADEZBDEZCDEZFZACGHZUGCBCIJZGHZKZAUHGHZ
    UDUEUGUJLUCUDUEKUIUGBCMNOUFUCUEUHDEZUJUKPUCUDUEQUCUDUERUDUEULUCBCSOACUHTUAU
    B $.
    $( [17-Nov-2012] $)

  $( If an integer divides either of two others, it divides their product.
     (Contributed by Paul Chapman, 17-Nov-2012.)  (The proof was shortened by
     Mario Carneiro, 17-Jul-2014.) $)
  ordvdsmul $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || M \/ K || N ) -> K || ( M x. N ) ) ) $=
    ( cz wcel w3a cdivides wbr cmul co dvdsmultr1 dvdsmultr2 jaod ) ADEBDECDEFA
    BGHABCIJGHACGHABCKABCLM $.
    $( [17-Jul-2014] $) $( [17-Nov-2012] $)

  $( If an integer divides a difference, then it divides one term iff it
     divides the other. $)
  dvdssub2 $p |- ( ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) /\ K || ( M - N ) ) ->
                   ( K || M <-> K || N ) ) $=
    ( cz wcel cmin co cdivides wbr wa wi 3adant1 imp wceq cc zcn syl2an breqtrd
    adantr expr w3a zsubcl dvds2sub syld3an3 ancomsd nncan caddc dvds2add npcan
    syld3an2 impbid ) ADEZBDEZCDEZUAZABCFGZHIZJABHIZACHIZUOUQURUSUOUQURJZJABUPF
    GZCHUOUTAVAHIZUOURUQVBULUMUNUPDEZURUQJVBKUMUNVCULBCUBLZABUPUCUDUEMUOVACNZUT
    UMUNVEULUMBOEZCOEZVEUNBPZCPZBCUFQLSRTUOUQUSURUOUQUSJZJAUPCUGGZBHUOVJAVKHIZU
    LVCUMUNVJVLKVDAUPCUHUJMUOVKBNZVJUMUNVMULUMVFVGVMUNVHVIBCUIQLSRTUK $.
    $( [13-Jul-2014] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsadd $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M + N ) ) ) $=
    ( cz wcel wa caddc co cdivides wbr cmin wb simpl zaddcl simpr iddvds adantr
    cc wceq zcn pncan syl2an breqtrrd dvdssub2 syl31anc bicomd ) ACDZBCDZEZAABF
    GZHIZABHIZUHUFUICDUGAUIBJGZHIUJUKKUFUGLABMUFUGNUHAAULHUFAAHIUGAOPUFAQDBQDUL
    ARUGASBSABTUAUBAUIBUCUDUE $.
    $( [13-Jul-2014] $) $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their sum.  (Contributed by Paul
     Chapman, 31-Mar-2011.) $)
  dvdsaddr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N + M ) ) ) $=
    ( cz wcel wa cdivides wbr caddc co dvdsadd cc wceq zcn addcom syl2an breq2d
    bitrd ) ACDZBCDZEZABFGAABHIZFGABAHIZFGABJTUAUBAFRAKDBKDUAUBLSAMBMABNOPQ $.
    $( [31-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssub $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( M - N ) ) ) $=
    ( cz wcel wa cdivides wbr cneg caddc co cmin dvdsnegb znegcl dvdsadd sylan2
    wb cc wceq zcn negsub syl2an breq2d 3bitrd ) ACDZBCDZEZABFGABHZFGZAAUGIJZFG
    ZAABKJZFGABLUEUDUGCDUHUJPBMAUGNOUFUIUKAFUDAQDBQDUIUKRUEASBSABTUAUBUC $.
    $( [21-Mar-2011] $)

  $( An integer divides another iff it divides their difference.  (Contributed
     by Paul Chapman, 31-Mar-2011.) $)
  dvdssubr $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( M || N <-> M || ( N - M ) ) ) $=
    ( cz wcel wa cmin co cdivides caddc wb zsubcl ancoms dvdsadd syldan cc wceq
    wbr zcn pncan3 syl2an breq2d bitr2d ) ACDZBCDZEZABAFGZHQZAAUFIGZHQZABHQUCUD
    UFCDZUGUIJUDUCUJBAKLAUFMNUEUHBAHUCAODBODUHBPUDARBRABSTUAUB $.
    $( [31-Mar-2011] $)

  ${
    $d k A $.  $d k N $.  $d k ph $.
    fsumdvds.1 $e |- ( ph -> A e. Fin ) $.
    fsumdvds.2 $e |- ( ph -> N e. ZZ ) $.
    fsumdvds.3 $e |- ( ( ph /\ k e. A ) -> B e. ZZ ) $.
    fsumdvds.4 $e |- ( ( ph /\ k e. A ) -> N || B ) $.
    $( If every term in a sum is divisible by ` N ` , then so is the sum. $)
    fsumdvds $p |- ( ph -> N || sum_ k e. A B ) $=
      ( csu cdivides wbr cc0 wceq wa cz wcel adantlr syl adantr dvds0 ax-mp a1i
      0z simpr cv simplr eqbrtrrd wb 0dvds mpbid sumeq2dv cuz cfv wss olcd sumz
      cfn wo eqtrd 3brtr4d wne cdiv co cc zcn fsumdivc divides2 syl3anc fsumzcl
      eqeltrd mpbird pm2.61dane ) AEBCDJZKLZEMAEMNZOZMMEVNKMMKLZVQMPQVRUDMUAUBU
      CAVPUEVQVNBMDJZMVQBCMDVQDUFBQZOZMCKLZCMNZWAEMCKAVPVTUGAVTECKLZVPIRUHWACPQ
      ZWBWCUIAVTWEVPHRCUJSUKULVQBMUMUNUOZBURQZUSVSMNVQWGWFAWGVPFTUPBDMUQSUTVAAE
      MVBZOZVOVNEVCVDZPQZWIWJBCEVCVDZDJPWIBCEDAWGWHFTZWIEPQZEVEQAWNWHGTZEVFSWIV
      TOZWECVEQAVTWEWHHRZCVFSAWHUEZVGWIBWLDWMWPWDWLPQZAVTWDWHIRWPWNWHWEWDWSUIWI
      WNVTWOTWIWHVTWRTWQECVHVIUKVJVKWIWNWHVNPQZVOWKUIWOWRAWTWHABCDFHVJTEVNVHVIV
      LVM $.
      $( [17-Jan-2015] $)
  $}

  ${
    dvdslelem.1 $e |- M e. ZZ $.
    dvdslelem.2 $e |- N e. NN $.
    dvdslelem.3 $e |- K e. ZZ $.
    $( Lemma for ~ dvdsle . $)
    dvdslelem $p |- ( N < M -> ( K x. M ) =/= N ) $=
      ( clt wbr cmul co wo cc0 cle cr wcel zrei 0re wb wa ax-mp wne c1 lelttric
      mp2an cz wi cn elnnz elnnz1 bitr3i pm5.32 orbi2i mpbi cneg le0neg1 nngt0i
      mpbir nnrei lttri mpan syl renegcli mulge0i sylan2 sylanb expcom remulcli
      ltlei recni mulneg1i breq2i bitr4i syl6ibr lelttri syl6 lemulge12 mpanl12
      mpan2 sylan ex ltletri syld orim12d mpi lttri2i sylibr ) CBGHZABIJZCGHZCW
      HGHZKZWHCUAWGALMHZUBAMHZKZWKWLLAGHZKZWNANOZLNOWPAFPZQALUCUDWOWMWLAUEOZWOW
      MRZFWSWTUFWSWOSZWSWMSZRXAAUGOXBAUHAUIUJWSWOWMUKUQTULUMWGWLWIWMWJWGWLWHLMH
      ZWIWGWLLAUNZBIJZMHZXCWLWGXFWLLXDMHZWGXFWQWLXGRWRAUOTWGXGLBMHZXFWGLBGHZXHL
      CGHZWGXICEUPZLCBQCEURZBDPZUSUTLBQXMVHVAZXDBAWRVBXMVCVDVEVFXCLWHUNZMHZXFWH
      NOXCXPRABWRXMVGZWHUOTXEXOLMABAWRVIBXMVIVJVKVLVMXCXJWIXKWHLCXQQXLVNVRVOWGW
      MBWHMHZWJWGWMXRWGXHWMXRXNBNOWQXHWMSXRXMWRBAVPVQVSVTWGXRWJCBWHXLXMXQWAVTWB
      WCWDWHCXQXLWEWF $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( The divisors of a positive integer are bounded by it.  The proof does
       not use ` / ` .  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    dvdsle $p |- ( ( M e. ZZ /\ N e. NN ) -> ( M || N -> M <_ N ) ) $=
      ( vn cz wcel cn wa wbr clt wn cmul co wne wi c1 cif neeq1d imbi12d elimel
      wceq cdivides cle w3a cv wrex breq2 oveq2 breq1 neeq2 oveq1 imbi2d 1z 1nn
      dvdslelem dedth3h 3expia com23 3impia imp df-ne nrexdv nnz divides sylan2
      sylib wb 3adant3 mtbird con2d cr zre nnre lenlt syl2an sylibrd ) ADEZBFEZ
      GZABUAHZBAIHZJZABUBHZVRVTVSVPVQVTVSJVPVQVTUCZVSCUDZAKLZBTZCDUEZWCWFCDWCWD
      DEZGWEBMZWFJWCWHWIVPVQVTWHWINVRWHVTWIVPVQWHVTWINZVPVQWHWJBVPAOPZIHZWDWKKL
      ZBMZNVQBOPZWKIHZWMWOMZNWPWHWDOPZWKKLZWOMZNABWDOOOAWKTZVTWLWIWNAWKBIUFXAWE
      WMBAWKWDKUGQRBWOTWLWPWNWQBWOWKIUHBWOWMUIRWDWRTZWQWTWPXBWMWSWOWDWRWKKUJQUK
      WRWKWOAODULSBOFUMSWDODULSUNUOUPUQURUSWEBUTVEVAVPVQVSWGVFZVTVQVPBDEXCBVBCA
      BVCVDVGVHUPVIVPAVJEBVJEWBWAVFVQAVKBVLABVMVNVO $.
      $( [21-Mar-2011] $)
  $}

  $( The divisors of a nonzero integer are bounded by its absolute value.
     (Contributed by Paul Chapman, 21-Mar-2011.) $)
  dvdsleabs $p |- ( ( M e. ZZ /\ N e. ZZ /\ N =/= 0 ) ->
                    ( M || N -> M <_ ( abs ` N ) ) ) $=
    ( cz wcel cc0 wne w3a cdivides wbr cabs cfv wb wa wceq cneg wo adantl breq2
    cle wi cr zre absor syl a1i dvdsnegb adantr bitr4d jaod mpd 3adant3 nnabscl
    ex cn dvdsle sylan2 3impb sylbird ) ACDZBCDZBEFZGABHIZABJKZHIZAVCSIZUSUTVDV
    BLZVAUSUTMZVCBNZVCBOZNZPZVFUTVKUSUTBUADVKBUBBUCUDQVGVHVFVJVHVFTVGVCBAHRUEVG
    VJVFVGVJMVDAVIHIZVBVJVDVLLVGVCVIAHRQVGVBVLLVJABUFUGUHUMUIUJUKUSUTVAVDVETZUT
    VAMUSVCUNDVMBULAVCUOUPUQUR $.
    $( [21-Mar-2011] $)

  $( If two integers divide each other, they must be equal, up to a difference
     in sign. $)
  dvdseq $p |- ( ( ( M e. NN0 /\ N e. NN0 ) /\ ( M || N /\ N || M ) ) ->
                    M = N ) $=
    ( cn0 wcel wa cdivides wbr wceq cn cc0 wo elnn0 cle dvdsle syl2anc ad2antlr
    wi cz mpd wb simprl nn0z ad2antrr simplr simprr nnz wne nnne0 breq1 biimpcd
    wn ad2antrl 0dvds syl sylibd necon3ad simpll sylib ord mt3d cr nn0re letri3
    nnre syl2an adantr mpbir2and ex simpr sylan9bbr eqeq2d bitr4d syl5ib jaodan
    sylan2b imp ) ACDZBCDZEABFGZBAFGZEZABHZVRVQBIDZBJHZKWAWBQZBLVQWCWEWDVQWCEZW
    AWBWFWAEZWBABMGZBAMGZWGVSWHWFVSVTUAWGARDZWCVSWHQVQWJWCWAAUBZUCVQWCWAUDABNOS
    WGVTWIWFVSVTUEWGBRDZAIDZVTWIQWCWLVQWABUFPZWGWMAJHZWGBJUGZWOUKWCWPVQWABUHPWG
    WOBJWGWOJBFGZWDVSWOWQQWFVTWOVSWQAJBFUIUJULWGWLWQWDTWNBUMUNUOUPSWGWMWOWGVQWM
    WOKVQWCWAUQALURUSUTBANOSWFWBWHWIETZWAVQAVADBVADWRWCAVBBVDABVCVEVFVGVHWAVTVQ
    WDEZWBVSVTVIWSVTWOWBWDVTJAFGZVQWOBJAFUIVQWJWTWOTWKAUMUNVJWSBJAVQWDVIVKVLVMV
    NVOVP $.
    $( [30-May-2014] $)

  ${
    $d N x $.
    $( Only 0 is divisible by all integers.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    alzdvds $p |- ( N e. ZZ -> ( A. x e. ZZ x || N <-> N = 0 ) ) $=
      ( cz wcel cv cdivides wbr wral cc0 wceq wa wne wn wrex cr cn syl ralrimiv
      wi expcom cabs cfv cle clt zcn abscl arch wss nnssz ssrexv ax-mp 3syl zre
      cc ltnle syl2an rexbidva rexnal syl6bb mpbid adantl dvdsleabs 3expb ralim
      wb com12 expdimp mtod nne sylib dvds0 breq2 syl5ibr impbid1 ) BCDZAEZBFGZ
      ACHZBIJZVRVOVSVRVOKZBILZMVSVTWAVPBUAUBZUCGZACHZVOWDMZVRVOWBVPUDGZACNZWEVO
      WBODZWFAPNZWGVOBUNDWHBUEBUFQZWBAUGPCUHWIWGSUIWFAPCUJUKULVOWGWCMZACNWEVOWF
      WKACVOWHVPODWFWKVEVPCDZWJVPUMWBVPUOUPUQWCACURUSUTVAVRVOWAWDVOWAKZVRWDWMVQ
      WCSZACHVRWDSWMWNACWLWMWNWLVOWAWNVPBVBVCTRVQWCACVDQVFVGVHBIVIVJTVSVQACWLVQ
      VSVPIFGVPVKBIVPFVLVMRVN $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d K x y $.  $d N x $.
    $( A positive integer divides any greater factorial.  (Contributed by Paul
       Chapman, 28-Nov-2012.) $)
    dvdsfac $p |- ( ( K e. NN /\ N e. ( ZZ>= ` K ) ) -> K || ( ! ` N ) ) $=
      ( vx vy cfv wcel cn cfa cdivides wbr wi c1 co wceq fveq2 breq2d imbi2d cz
      nnz 3syl cuz cv caddc cmin cmul cn0 nnm1nn0 faccl dvdsmul2 syl2anc facnn2
      breqtrrd a1i wa adantl elnnuz uztrn sylan2b sylibr syl peano2z dvdsmultr1
      nnnn0 syl3anc facp1 sylibrd ex a2d uzind4 impcom ) BAUAEZFAGFZABHEZIJZVLA
      CUBZHEZIJZKVLAAHEZIJZKZVLADUBZHEZIJZKVLAWALUCMZHEZIJZKVLVNKCDABVOANZVQVSV
      LWGVPVRAIVOAHOPQVOWANZVQWCVLWHVPWBAIVOWAHOPQVOWDNZVQWFVLWIVPWEAIVOWDHOPQV
      OBNZVQVNVLWJVPVMAIVOBHOPQVTARFZVLAALUDMZHEZAUEMZVRIVLWMRFZWKAWNIJVLWLUFFW
      MGFWOAUGWLUHWMSTASZWMAUIUJAUKULUMWAVKFZVLWCWFWQVLWCWFKWQVLUNZWCAWBWDUEMZI
      JZWFWRWKWBRFZWDRFZWCWTKVLWKWQWPUOWRWBGFZXAWRWAGFZWAUFFZXCWRWALUAEZFZXDVLW
      QAXFFXGAUPAWALUQURWAUPUSZWAVCZWAUHTWBSUTWRXDWARFXBXHWASWAVATAWBWDVBVDWRWE
      WSAIWRXDXEWEWSNXHXIWAVETPVFVGVHVIVJ $.
      $( [28-Nov-2012] $)
  $}

  ${
    $d K x y $.  $d N x $.
    $( A power divides a power with a greater exponent.  (Contributed by Mario
       Carneiro, 23-Feb-2014.) $)
    dvdsexp $p |- ( ( A e. ZZ /\ M e. NN0 /\ N e. ( ZZ>= ` M ) ) ->
                    ( A ^ M ) || ( A ^ N ) ) $=
      ( cz wcel cn0 cuz cfv w3a cexp co cdivides wbr 3ad2ant3 zexpcl syl2anc cc
      wceq zcn syl cmin cmul simp1 cle eluz2 znn0sub2 sylbi 3adant3 caddc simp2
      dvdsmul2 expadd syl3anc eluzelz nn0cn npcan oveq2d eqtr3d breqtrd ) ADEZB
      FEZCBGHEZIZABJKZACBUAKZJKZVDUBKZACJKZLVCVFDEZVDDEZVDVGLMVCUTVEFEZVIUTVAVB
      UCZVBUTVKVAVBBDECDEZBCUDMIVKBCUEBCUFUGNZAVEOPUTVAVJVBABOUHVFVDUKPVCAVEBUI
      KZJKZVGVHVCAQEZVKVAVPVGRVCUTVQVLASTVNUTVAVBUJZAVEBULUMVCVOCAJVCCQEZBQEZVO
      CRVBUTVSVAVBVMVSBCUNCSTNVCVAVTVRBUOTCBUPPUQURUS $.
      $( [23-Feb-2014] $)
  $}

  ${
    $( Any number ` K ` whose mod base ` N ` is divisible by a divisor ` P ` of
       the base is also divisible by ` P ` .  This means that primes will also
       be relatively prime to the base when reduced ` mod N ` for any base.
       (Contributed by Mario Carneiro, 13-Mar-2014.) $)
    dvdsmod $p |- ( ( ( P e. NN /\ N e. NN /\ K e. ZZ ) /\ P || N ) ->
                    ( P || ( K mod N ) <-> P || K ) ) $=
      ( cn wcel cz cdivides wbr cmo co cmin cc0 wceq syl syl2anc breq2d syl3anc
      cr wb moddvds w3a wa cdiv cfl cfv cmul crp simpl3 zre simpl2 modval simpr
      nnrp wi simpl1 nnz nndivre flcl dvdsmultr1 mpd zmulcl zcn subid1 breqtrrd
      cc 3syl 0z a1i mpbird eqeq2d 3bitr3d recnd 3bitrd ) ADEZCDEZBFEZUAZACGHZU
      BZABCIJZGHABCBCUCJZUDUEZUFJZKJZGHZABLKJZGHZABGHVSVTWDAGVSBREZCUGEZVTWDMVS
      VPWHVNVOVPVRUHZBUINZVSVOWIVNVOVPVRUJZCUMNBCUKOPVSBAIJZWCAIJZMZWMLAIJZMZWE
      WGVSWNWPWMVSWNWPMZAWCLKJZGHZVSAWCWSGVSVRAWCGHZVQVRULVSAFEZCFEZWBFEZVRXAUN
      VSVNXBVNVOVPVRUOZAUPNVSVOXCWLCUPNZVSWAREZXDVSWHVOXGWKWLBCUQOWAURNZACWBUSQ
      UTVSWCFEZWCVEEWSWCMVSXCXDXIXFXHCWBVAOZWCVBWCVCVFVDVSVNXILFEZWRWTSXEXJXKVS
      VGVHZWCLATQVIVJVSVNVPXIWOWESXEWJXJBWCATQVSVNVPXKWQWGSXEWJXLBLATQVKVSWFBAG
      VSBVEEWFBMVSBWKVLBVCNPVM $.
      $( [13-Mar-2014] $)
  $}

  ${
    $d j k $.  $d j m $.  $d j n $.  $d j x $.  $d j y $.  $d k m $.  $d k y $.
    $d m n $.  $d m x $.  $d m y $.  $d N j $.  $d N k $.  $d N n $.  $d n x $.
    $d n y $.  $d k x $.
    $( Lemma for ~ odd2np1 .  (Contributed by Scott Fenton, 03-Apr-2014.) $)
    odd2np1lem $p |- ( N e. NN0 -> (
      E. n e. ZZ ( ( 2 x. n ) + 1 ) = N \/ E. k e. ZZ ( k x. 2 ) = N ) ) $=
      ( vy c2 cv cmul co c1 caddc wceq cz wrex wo cc0 eqeq2 rexbidv eqeq1d wcel
      2cn vj vx vm orbi12d weq oveq2 oveq1d cbvrexv syl6bb oveq1 mul02i rcla4ev
      0z mp2an olci cn0 orcom wa cc zcn mulcom sylancl adantl eqid mpan2 eqeq2d
      syl5ibcom sylbid rexlimdva peano2z mulid2i a1i oveq12d df-2 oveq2i syl6eq
      wi ax-1cn adddir mp3an23 mulcl mpan addass 3eqtr4d syl2anc orim12d syl5bi
      syl nn0ind ) EBFZGHZIJHZUAFZKZBLMZAFZEGHZWMKZALMZNWLOKZBLMZWQOKZALMZNEUBF
      ZGHZIJHZUCFZKZUBLMZDFZEGHZXGKZDLMZNZWLXGIJHZKZBLMZWQXOKZALMZNZWLCKZBLMZWQ
      CKZALMZNUAUCCWMOKZWOXAWSXCYEWNWTBLWMOWLPQYEWRXBALWMOWQPQUDUAUCUEZWOXIWSXM
      YFWOWLXGKZBLMXIYFWNYGBLWMXGWLPQYGXHBUBLBUBUEZWLXFXGYHWKXEIJWJXDEGUFUGRUHU
      IYFWSWQXGKZALMXMYFWRYIALWMXGWQPQYIXLADLADUEWQXKXGWPXJEGUJRUHUIUDWMXOKZWOX
      QWSXSYJWNXPBLWMXOWLPQYJWRXRALWMXOWQPQUDWMCKZWOYBWSYDYKWNYABLWMCWLPQYKWRYC
      ALWMCWQPQUDXCXAOLSOEGHZOKZXCUMETUKXBYMAOLWPOKWQYLOWPOEGUJRULUNUOXNXMXINXG
      UPSZXTXIXMUQYNXMXQXIXSYNXLXQDLYNXJLSZURZXLEXJGHZXGKZXQYPXKYQXGYOXKYQKZYNY
      OXJUSSEUSSZYSXJUTTXJEVAVBVCRYOYRXQVQYNYOWLYQIJHZKZBLMZYRXQYOUUAUUAKZUUCUU
      AVDUUBUUDBXJLBDUEZWLUUAUUAUUEWKYQIJWJXJEGUFUGRULVEYRUUBXPBLYRUUAXOWLYQXGI
      JUJVFQVGVCVHVIYNXHXSUBLYNXDLSZURZWQXFIJHZKZALMZXHXSUUGXDIJHZLSZUUKEGHZUUH
      KZUUJUUFUULYNXDVJVCUUFUUNYNUUFXDUSSZUUNXDUTUUOXDEGHZIEGHZJHZXEIIJHZJHZUUM
      UUHUUOUURXEEJHUUTUUOUUPXEUUQEJUUOYTUUPXEKTXDEVAVEUUQEKUUOETVKVLVMEUUSXEJV
      NVOVPUUOIUSSZYTUUMUURKVRTXDIEVSVTUUOXEUSSZUUHUUTKZYTUUOUVBTEXDWAWBUVBUVAU
      VAUVCVRVRXEIIWCVTWHWDWHVCUUIUUNAUUKLWPUUKKWQUUMUUHWPUUKEGUJRULWEXHUUIXRAL
      XHUUHXOWQXFXGIJUJVFQVGVIWFWGWI $.
      $( [19-Apr-2014] $) $( [3-Apr-2014] $)
  $}

  ${
    $d N n k x y $.
    $( An integer is odd iff it is one plus twice another integer.
       (Contributed by Scott Fenton, 03-Apr-2014.) $)
    odd2np1 $p |- ( N e. ZZ ->
     ( -. 2 || N <-> E. n e. ZZ ( ( 2 x. n ) + 1 ) = N ) ) $=
      ( vk cz wcel c2 cmul co wceq wrex c1 caddc wb wa cneg syl 2cn cmin ax-1cn
      cc vx vy cdivides wbr wn cv 2z divides notbid wo cr cn0 elznn0 odd2np1lem
      mpan adantl peano2z znegcl ad2antlr zcn mulcl simpl recnd negcon2 syl2anc
      peano2cn sylancr negsubdi sylancl mulcli addsubass mp3an23 mulid1i oveq1i
      eqcom 1p1e2 subaddrii oveq2i syl6req mp3an13 oveq1d eqtr4d negeqd mulneg2
      eqtri adddi 3eqtr4d eqeq1d syl5bb bitrd biimpa oveq2 rcla4ev ex rexlimdva
      recn syl2anr mulneg1 syl6rbbr oveq1 orim12d syl5 jaodan sylbi cdiv halfnz
      imp reeanv eqtr3 mulcom eqeq2d subadd mp3an3 subcl cc0 2ne0 pm3.2i divmul
      wne ancoms subdi mp3an1 syl2an wi zsubcl eleq1 syl5ibcom sylbid rexlimivv
      w3a sylbird sylbir mto jctir xor3 xor2 bicom 3bitr3i sylib ) BDEZFBUCUDZU
      ECUFZFGHZBIZCDJZUEZFAUFZGHZKLHZBIZADJZYTUUAUUEFDEYTUUAUUEMUGCFBUHUOUIYTUU
      KUUEUJZUUKUUENZUEZNZUUFUUKMZYTUULUUNYTBUKEZBULEZBOZULEZUJNUULBUMUUQUURUUL
      UUTUURUULUUQCABUNUPUUQUUTUULUUTFUAUFZGHZKLHZUUSIZUADJZUBUFZFGHZUUSIZUBDJZ
      UJUUQUULUBUAUUSUNUUQUVEUUKUVIUUEUUQUVDUUKUADUUQUVADEZNZUVDUUKUVKUVDNUVAKL
      HZOZDEZFUVMGHZKLHZBIZUUKUVJUVNUUQUVDUVJUVLDEZUVNUVAUQZUVLURPUSUVKUVDUVQUV
      KUVDBUVCOZIZUVQUVKUVCTEZBTEZUVDUWAMUVJUWBUUQUVJUVATEZUWBUVAUTZUWDUVBTEZUW
      BFTEZUWDUWFQFUVAVAZUOUVBVFPPUPUVKBUUQUVJVBVCUVCBVDVEUWAUVTBIUVKUVQBUVTVOU
      VKUVTUVPBUVJUVTUVPIUUQUVJFUVLGHZKRHZOZUWIOZKLHZUVTUVPUVJUWITEZKTEZUWKUWMI
      UVJUWGUVLTEZUWNQUVJUVRUWPUVSUVLUTPZFUVLVAVGSUWIKVHVIUVJUVCUWJUVJUVCUVBFKG
      HZLHZKRHZUWJUVJUWTUVBUWRKRHZLHZUVCUVJUWFUWTUXBIZUVJUWGUWDUWFQUWEUWHVGUWFU
      WRTEUWOUXCFKQSVJSUVBUWRKVKVLPUXAKUVBLUXAFKRHKUWRFKRFQVMVNFKKQSSVPVQWEVRVS
      UVJUWIUWSKRUVJUWDUWIUWSIZUWEUWGUWDUWOUXDQSFUVAKWFVTPWAWBWCUVJUVOUWLKLUVJU
      WGUWPUVOUWLIQUWQFUVLWDVGWAWGUPWHWIWJWKUUJUVQAUVMDUUGUVMIZUUIUVPBUXEUUHUVO
      KLUUGUVMFGWLWAWHWMVEWNWOUUQUVHUUEUBDUUQUVFDEZNZUVHUUEUXGUVHNUVFOZDEZUXHFG
      HZBIZUUEUXFUXIUUQUVHUVFURUSUXGUVHUXKUXGUVHBUVGOZIZUXKUXFUVGTEZUWCUVHUXMMU
      UQUXFUVFTEZUWGUXNUVFUTZQUVFFVAVIBWPUVGBVDWQUXGUXKUXLBIUXMUXGUXJUXLBUXFUXJ
      UXLIZUUQUXFUXOUWGUXQUXPQUVFFWRVIUPWHBUXLVOWSWJWKUUDUXKCUXHDUUBUXHIUUCUXJB
      UUBUXHFGWTWHWMVEWNWOXAXBXGXCXDUUMKFXEHZDEZXFUUMUUJUUDNZCDJADJUXSUUJUUDACD
      DXHUXTUXSACDDUXTUUIUUCIZUUGDEZUUBDEZNZUXSUUIUUCBXIUYDUYAUUIFUUBGHZIZUXSUY
      CUYAUYFMUYBUYCUUCUYEUUIUYCUUBTEZUWGUUCUYEIUUBUTZQUUBFXJVIXKUPUYDUYFUYEUUH
      RHZKIZUXSUYCUYETEZUUHTEZUYJUYFMZUYBUYCUWGUYGUYKQUYHFUUBVAVGUYBUWGUUGTEZUY
      LQUUGUTZFUUGVAVGUYKUYLUWOUYMSUYEUUHKXLXMWQUYDUYJUUBUUGRHZUXRIZUXSUYBUYNUY
      GUYQUYJMUYCUYOUYHUYNUYGNZUYQFUYPGHZKIZUYJUYGUYNUYQUYTMZUYGUYNNUYPTEZVUAUU
      BUUGXNUWOVUBUWGFXOXSZNZVUASUWGVUCQXPXQUYQUXRUYPIUWOVUBVUDYJUYTUYPUXRVOKUY
      PFXRWIVTPXTUYRUYSUYIKUYGUYNUYSUYIIZUWGUYGUYNVUEQFUUBUUGYAYBXTWHWJYCUYCUYB
      UYQUXSYDUYCUYBNUYPDEUYQUXSUUBUUGYEUYPUXRDYFYGXTYKYKYHXBYIYLYMYNUUKUUEMUEU
      UKUUFMUUOUUPUUKUUEYOUUKUUEYPUUKUUFYQYRYSWJ $.
      $( [19-Apr-2014] $) $( [3-Apr-2014] $)
  $}

  ${
    $d j k x y F $.  $d j k x y N $.
    $( A rule for divisibility by 3 of a number written in base 10.
       (Contributed by Mario Carneiro, 14-Jul-2014.) $)
    3dvds $p |- ( ( N e. NN0 /\ F : ( 0 ... N ) --> ZZ ) ->
     ( 3 || sum_ k e. ( 0 ... N ) ( ( F ` k ) x. ( 10 ^ k ) ) <->
       3 || sum_ k e. ( 0 ... N ) ( F ` k ) ) ) $=
      ( vj wcel cc0 co cz c3 c10 cmul cmin cdivides wbr sylancr c1 c9 ax-1cn cc
      a1i cn0 cfz wf wa cv cfv cexp csu nnzi fzfid ffvelrn adantll 10nn elfznn0
      wb 3nn adantl zexpcl zmulcl syl2anc fsumzcl zsubcl caddc nncni negsubdi2i
      cneg df-10 oveq1i wceq 9nn pncan mp2an 3eqtri 3t3e9 eqtr4i cdiv wne 1lt10
      10re ltneii id geoser eqeltrrd 1z necomi subeq0i necon3bii mpbir divides2
      1re syl3anc mpbird zsscn negsubdi2 sylancl breqtrrd peano2zm syl dvdsnegb
      sseldi negdvdsb mpbid syl5eqbrr wi muldvds1 dvdsmultr2 subdi mulid1 eqtrd
      mpd oveq2d breqtrd fsumdvds fsumsub dvdssub2 syl31anc ) CUAEZFCUBGZHBUCZU
      DZIHEZXRAUEZBUFZJYBUGGZKGZAUHZHEXRYCAUHZHEIYFYGLGZMNIYFMNIYGMNUOYAXTIUPUI
      ZTZXTXRYEAXTFCUJZXTYBXREZUDZYCHEZYDHEZYEHEZXSYLYNXQXRHYBBUKULZYMJHEZYBUAE
      ZYOJUMUIZYLYSXTYBCUNUQZJYBURZOZYCYDUSUTZVAXTXRYCAYKYQVAXTIXRYEYCLGZAUHYHM
      XTXRUUEAIYKYJYMYPYNUUEHEUUDYQYEYCVBUTYMIYCYDPLGZKGZUUEMYMIUUFMNZIUUGMNZYM
      YSUUHUUAYSIIKGZUUFMNZUUHYSUUJPJLGZVFZUUFMUUMQUUJUUMJPLGQPVCGZPLGZQPJRJUMV
      DZVEJUUNPLVGVHQSEPSEZUUOQVIQVJVDRQPVKVLVMVNVOYSUULUUFMNZUUMUUFMNZYSUURUUL
      UUFVFZMNZYSUULPYDLGZUUTMYSUULUVBMNZUVBUULVPGZHEZYSFYBPLGZUBGZJDUEZUGGZDUH
      UVDHYSJDYBJSEYSUUPTJPVQYSPJWJVSVRVTZTYSWAZWBYSUVGUVIDYSFUVFUJYSUVHUVGEZUD
      YRUVHUAEZUVIHEYTUVLUVMYSUVHUVFUNUQJUVHUROVAWCYSUULHEZUULFVQZUVBHEZUVCUVEU
      OUVNYSPHEZYRUVNWDYTPJVBVLZTUVOYSUVOPJVQJPUVJWEUULFPJPJRUUPWFWGWHTYSUVQYOU
      VPWDYSYRYSYOYTUVKUUBOZPYDVBOUULUVBWIWKWLYSYDSEZUUQUUTUVBVIYSHSYDWMUVSWTRY
      DPWNWOWPYSUVNUUFHEZUURUVAUOUVRYSYOUWAUVSYDWQZWRZUULUUFWSOWLYSUVNUWAUURUUS
      UOUVRUWCUULUUFXAOXBXCYSYAYAUWAUUKUUHXDYAYSYITZUWDUWCIIUUFXEWKXJWRYMYAYNUW
      AUUHUUIXDYAYMYITYQYMYOUWAUUCUWBWRIYCUUFXFWKXJYMUUGYEYCPKGZLGZUUEYMYCSEZUV
      TUUQUUGUWFVIYMHSYCWMYQWTZYMHSYDWMUUCWTUUQYMRTYCYDPXGWKYMUWEYCYELYMUWGUWEY
      CVIUWHYCXHWRXKXIXLXMXTXRYEYCAYKYMHSYEWMUUDWTUWHXNXLIYFYGXOXP $.
      $( [17-Jan-2015] $) $( [14-Jul-2014] $)
  $}

