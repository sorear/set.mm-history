$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                         Fundamental theorem of arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d e f g k n p q x y z $.  $d q x y F $.  $d e f g q x y M $.  $d q y ph $.
    $d n p q x G $.  $d n p q x N $.  $d p P $.  $d f g n q x y R $.
    1arith.1 $e |- M = ( n e. NN |-> ( p e. Prime |-> ( p pCnt n ) ) ) $.
    $( Lemma for ~ 1arith . $)
    1arithlem1 $p |- ( N e. NN ->
                       ( M ` N ) = ( p e. Prime |-> ( p pCnt N ) ) ) $=
      ( cprime cv cpc co cmpt cn wceq wcel wa simpl oveq2d mpteq2dva zex prmz
      cz ssriv ssexi mptex fvmpt ) ACDFDGZAGZHIZJDFUECHIZJKBUFCLZDFUGUHUIUEFMZN
      UFCUEHUIUJOPQEDFUHFTRAFTUFSUAUBUCUD $.
      $( [30-May-2014] $)

    $( Lemma for ~ 1arith . $)
    1arithlem2 $p |- ( ( N e. NN /\ P e. Prime ) ->
                       ( ( M ` N ) ` P ) = ( P pCnt N ) ) $=
      ( cn wcel cprime cfv cv cpc cmpt 1arithlem1 fveq1d oveq1 eqid ovex fvmpt
      co sylan9eq ) DGHZAIHADCJZJAEIEKZDLTZMZJADLTZUBAUCUFBCDEFNOEAUEUGIUFUDADL
      PUFQADLRSUA $.
      $( [30-May-2014] $)

    $( Lemma for ~ 1arith . $)
    1arithlem3 $p |- ( N e. NN -> ( M ` N ) : Prime --> NN0 ) $=
      ( cn wcel cprime cn0 cfv wf cv cpc cmpt pccl ancoms eqid fmptd 1arithlem1
      co feq1d mpbird ) CFGZHICBJZKHIDHDLZCMTZNZKUCDHUFIUGUEHGUCUFIGUECOPUGQRUC
      HIUDUGABCDESUAUB $.
      $( [30-May-2014] $)

    ${
      1arithlem4.2 $e |- G = ( y e. NN |->
                               if ( y e. Prime , ( y ^ ( F ` y ) ) , 1 ) ) $.
      1arithlem4.3 $e |- ( ph -> F : Prime --> NN0 ) $.
      1arithlem4.4 $e |- ( ph -> N e. NN ) $.
      1arithlem4.5 $e |- ( ( ph /\ ( q e. Prime /\ N <_ q ) ) ->
                           ( F ` q ) = 0 ) $.
      $( Lemma for ~ 1arith . $)
      1arithlem4 $p |- ( ph -> E. x e. NN F = ( M ` x ) ) $=
        ( cfv cn wcel wceq cprime cmul c1 cseq cv wrex wf cn0 ffvelrn ralrimiva
        sylan pcmptcl simprd syl2anc wral cpc cle wbr cc0 cif 1arithlem2 adantr
        wa co simpr fveq2 pcmpt cr nnre syl prmz zre adantl ifid anassrs ifeq2d
        cz syl5reqr iftrue lecasei 3eqtrrd wb 1arithlem3 wfn ffn eqfnfv2 syl2an
        mpbird eqeq2d rcla4ev ) AHUAFUBUCZPZQRZEWKGPZSZEBUDZGPZSZBQUEAQQWJUFZHQ
        RZWLAQQFUFWRACUDZEPZCFLAXAUGRZCTATUGEUFZWTTRXBMTUGWTEUHUJUIZUKULNQQHWJU
        HUMZAWNIUDZEPZXFWMPZSZITUNZAXIITAXFTRZVBZXHXFWKUOVCZXFHUPUQZXGURUSZXGAW
        LXKXHXMSXEXFDGWKJKUTUJXLXAXGXFCFHLAXBCTUNXKXDVAAWSXKNVAZAXKVDWTXFEVEVFX
        LXOXGSZHXFXLWSHVGRXPHVHVIXKXFVGRZAXKXFVPRXRXFVJXFVKVIVLXLHXFUPUQZVBZXGX
        NXGXGUSXOXNXGVMXTXNXGURXGAXKXSXGURSOVNVOVQXNXQXLXNXGURVRVLVSVTUIAXCTUGW
        MUFZWNXJWAZMAWLYAXEDGWKJKWBVIXCETWCWMTWCYBYATUGEWDTUGWMWDITEWMWEWFUMWGW
        QWNBWKQWOWKSWPWMEWOWKGVEWHWIUM $.
        $( [30-May-2014] $)
    $}

    1arith.2 $e |- R = { e e. ( NN0 ^m Prime ) | ( `' e " NN ) e. Fin } $.
    $( Fundamental theorem of arithmetic, where a prime factorization is
       represented as a sequence of prime exponents, for which only finitely
       many primes have nonzero exponent.  The function ` M ` maps the set of
       positive integers one-to-one onto the set of prime factorizations
       ` R ` .  (Contributed by Paul Chapman, 17-Nov-2012.)  (The proof was
       shortened by Mario Carneiro, 30-May-2014.) $)
    1arith $p |- M : NN -1-1-onto-> R $=
      ( vx vq cn cv cfv wceq wcel cprime cn0 wa syl wbr cr vy vf vk vg wf1o wf1
      wfo df-f1o wf wi wral dff13 wfn ffnfv cpc co cmpt cvv fnmpt cz prmz ssriv
      zex ssexi mptex a1i mprg cmap ccnv cima cfn 1arithlem3 nn0ex elmap sylibr
      c1 cfz wss fzfi wb ffn elpreima 1arithlem2 eleq1d cdivides cle id syl2anr
      dvdsle pcelnn ancoms cuz prmnn nnuz syl6eleq elfz5 3imtr4d sylbid expimpd
      nnz ssrdv ssfi sylancr cnveq imaeq1d elrab2 sylanbrc rgen mpbir2an syl2an
      eqfnfv2 adantlr adantll eqeq12d ralbidva bitrd pc11 3expia wrex dffo3 cdm
      rgen2a cnvimass simplbi sylib fdm zssre sstri eqsstrd simprbi syl2anc cif
      syl5ss cc0 simplr 0re wn clt adantr mpd fimaxre2 cexp caddc eqid ad2antrr
      cfl ifcl sylancl max1 flge0nn0 nn0p1nn simprl sseli flltp1 lelttrd simprr
      nnre max2 ltletrd ltnle mpbid biantrurd bitr4d breq1 rcla4cv mtod ffvelrn
      wo elnn0 ord 1arithlem4 ex rexlimdva ) JADUEJADUFZJADUGZJADUHUVNJADUIZHKZ
      DLZUAKZDLZMZUVQUVSMZUJZUAJUKHJUKHUAJADULUVPDJUMZUVRANZHJUKHJADUNEOEKCKZUO
      UPZUQZURNZUWDCJCJUWHDURFUSUWIUWFJNEOUWGOUTVCIOUTIKZVAZVBZVDZVEVFVGUWEHJUV
      QJNZUVRPOVHUPZNZUVRVIZJVJZVKNZUWEUWNOPUVRUIZUWPCDUVQEFVLZPOUVRVMUWMVNVOUW
      NVPUVQVQUPZVKNUWRUXBVRUWSVPUVQVSUWNIUWRUXBUWNUWJUWRNZUWJONZUWJUVRLZJNZQZU
      WJUXBNZUWNUVROUMZUXCUXGVTUWNUWTUXIUXAOPUVRWAZROUWJJUVRWBRUWNUXDUXFUXHUWNU
      XDQZUXFUWJUVQUOUPZJNZUXHUXKUXEUXLJUWJCDUVQEFWCZWDUXKUWJUVQWESZUWJUVQWFSZU
      XMUXHUXDUWJUTNUWNUXOUXPUJUWNUWKUWNWGUWJUVQWIWHUXDUWNUXMUXOVTUWJUVQWJWKUXD
      UWJVPWLLZNUVQUTNUXHUXPVTUWNUXDUWJJUXQUWJWMWNWOUVQWTUWJVPUVQWPWHWQWRWSWRXA
      UXBUWRXBXCBKZVIZJVJZVKNZUWSBUVRUWOAUXRUVRMZUXTUWRVKUYBUXSUWQJUXRUVRXDXEWD
      GXFXGXHXIZUWCHUAJUWNUVSJNZQZUWAUXLUWJUVSUOUPZMZIOUKZUWBUYEUWAUXEUWJUVTLZM
      ZIOUKZUYHUWNUWTOPUVTUIZUWAUYKVTZUYDUXACDUVSEFVLUWTUXIUVTOUMUYMUYLUXJOPUVT
      WAIOUVRUVTXKXJXJUYEUYJUYGIOUYEUXDQUXEUXLUYIUYFUWNUXDUXEUXLMUYDUXNXLUYDUXD
      UYIUYFMUWNUWJCDUVSEFWCXMXNXOXPUWNUYDUYHUWBUVQUVSIXQXRWRYBXIUVOUVPUBKZUVRM
      HJXSZUBAUKHUBJADXTUYCUYOUBAUYNANZUCKZUVSWFSZUCUYNVIZJVJZUKZUATXSZUYOUYPUY
      TTVRUYTVKNZVUBUYPUYTUYNYAZTUYNJYCUYPVUDOTUYPOPUYNUIZVUDOMUYPUYNUWONZVUEUY
      PVUFVUCUYAVUCBUYNUWOAUXRUYNMZUXTUYTVKVUGUXSUYSJUXRUYNXDXEWDGXFZYDPOUYNVMU
      WMVNYEZOPUYNYFROTVRUYPOUTTUWLYGYHZVFYIYMUYPVUFVUCVUHYJUAUCUYTUUAYKUYPVUAU
      YOUATUYPUVSTNZQZVUAUYOVULVUAQZHUDCUYNUDJUDKZONVUNVUNUYNLUUBUPVPYLUQZDYNUV
      SWFSZUVSYNYLZUUFLZVPUUCUPZIEFVUOUUDUYPVUEVUKVUAVUIUUEZVUMVURPNZVUSJNZVUMV
      UQTNZYNVUQWFSZVVAVUMVUKYNTNZVVCUYPVUKVUAYOZYPVUPUVSYNTUUGUUHZVUMVVEVUKVVD
      YPVVFYNUVSUUIXCVUQUUJYKVURUUKRZVUMUXDVUSUWJWFSZQZQZUWJUYNLZJNZYQVVLYNMZVV
      KVVMUWJUVSWFSZVVKUVSUWJYRSZVVOYQZVVKUVSVUSUWJVUMVUKVVJVVFYSZVVKVVBVUSTNVU
      MVVBVVJVVHYSVUSUUQRZVVKUXDUWJTNZVUMUXDVVIUULZOTUWJVUJUUMRZVVKUVSVUQVUSVVR
      VUMVVCVVJVVGYSZVVSVVKVVEVUKUVSVUQWFSYPVVRYNUVSUURXCVVKVVCVUQVUSYRSVWCVUQU
      UNRUUOVUMUXDVVIUUPUUSVVKVUKVVTVVPVVQVTVVRVWBUVSUWJUUTYKUVAVVKVVMUWJUYTNZV
      VOVVKVVMUXDVVMQZVWDVVKUXDVVMVWAUVBVVKUYNOUMZVWDVWEVTVVKVUEVWFVUMVUEVVJVUT
      YSZOPUYNWAROUWJJUYNWBRUVCVVKVUAVWDVVOUJVULVUAVVJYOUYRVVOUCUWJUYTUYQUWJUVS
      WFUVDUVERWRUVFVVKVVMVVNVVKVVLPNZVVMVVNUVHVVKVUEUXDVWHVWGVWAOPUWJUYNUVGYKV
      VLUVIYEUVJYTUVKUVLUVMYTXHXIXI $.
      $( [30-May-2014] $) $( [17-Nov-2012] $)

    $( Fundamental theorem of arithmetic, where a prime factorization is
       represented as a finite monotonic 1-based sequence of primes.  Every
       positive integer has a unique prime factorization.  (Contributed by Paul
       Chapman, 17-Nov-2012.)  (Revised by Mario Carneiro, 30-May-2014.) $)
    1arith2 $p |- A. z e. NN E! g e. R ( M ` z ) = g $=
      ( cv cfv wceq wreu cn wcel ccnv wf1o 1arith f1ocnv ax-mp f1ocnvfvb mp3an1
      f1ofveu mpan wb reubidva mpbird rgen ) AJZFKDJZLZDBMZANUINOZULUJFPZKUILZD
      BMZBNUNQZUMUPNBFQZUQBCEFGHIRZNBFSTDBNUIUNUCUDUMUKUODBURUMUJBOUKUOUEUSNBUI
      UJFUAUBUFUGUH $.
      $( [30-May-2014] $) $( [17-Nov-2012] $)
  $}


