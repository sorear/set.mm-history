$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                    From groups to Hilbert spaces
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c +g $.
  $c GrpNEW $.
  $c 0g $.
  $c -g $.
  $c AbelNEW $.
  $c .r $.
  $c RingNEW $.
  $c 1rNEW $.
  $c C_rng $.
  $c DivRingNEW $.
  $c invr $.
  $c vbase $.
  $c vadd $.
  $c vsca $.
  $c LVec $.
  $c 0vNEW $.
  $c -vNEW $.
  $c RVecNEW $.
  $c +ss $.
  $c SubSpNEW $.
  $c spanNEW $.
  $c o+ $.
  $c Ev $.
  $c /v $.
  $c *v $.
  $c LVec* $.
  $c ip $.
  $c PreHil $.
  $c normv $.
  $c ocv $.
  $c CSubSp $.
  $c Hil $.

  $( Extend class notation with group operation. $)
  cplusg $a class +g $.

  $( Extend class notation with class of all groups. $)
  cgrp $a class GrpNEW $.

  $( Extend class notation with group identity element. $)
  c0g $a class 0g $.

  $( Extend class notation with inverse of group element. $)
  cminusg $a class -g $.

  $( Extend class notation with class of all Abelian groups. $)
  cabel $a class AbelNEW $.

  $( Extend class notation with ring multiplication. $)
  cmulr $a class .r $.

  $( Extend class notation with class of all rings. $)
  crg $a class RingNEW $.

  $( Extend class notation with ring unit. $)
  cur $a class 1rNEW $.

  $( Extend class notation subring. $)
  csrng $a class C_rng $.

  $( Extend class notation with class of all division rings. $)
  cdivring $a class DivRingNEW $.

  $( Extend class notation with multiplicative inverse. $)
  cinvr $a class invr $.

  $( Extend class notation with set of vectors in a left vector space. $)
  cvbase $a class vbase $.

  $( Extend class notation with vector addition. $)
  cvadd $a class vadd $.

  $( Extend class notation with scalar product. $)
  cvsca $a class vsca $.

  $( Extend class notation with class of all left vector spaces. $)
  clvec $a class LVec $.

  $( Extend class notation with zero vector. $)
  czv $a class 0vNEW $.

  $( Extend class notation with vector subtraction. $)
  cminusv $a class -vNEW $.

  $( Extend class notation with class of all right vector spaces. $)
  crvec $a class RVecNEW $.

  $( Extend class notation with subspace sum. $)
  cplusss $a class +ss $.

  $( Extend class notation with set of all subspaces in a vector space. $)
  cssp $a class SubSpNEW $.

  $( Extend class notation with span of a set of vectors. $)
  cspan $a class spanNEW $.

  $( Extend class notation with direct sum. $)
  coplus $a class o+ $.

  $( Extend class notation with vector equivalence classes. $)
  cev $a class Ev $.

  $( Extend class notation with quotient space. $)
  cqv $a class /v $.

  $( Extend class notation with involution. $)
  cstv $a class *v $.

  $( Extend class notation with class of all left vector spaces over
     *-fields. $)
  clvecs $a class LVec* $.

  $( Extend class notation with Hermitian form (inner product). $)
  cipr $a class ip $.

  $( Extend class notation with class all pre-Hilbert spaces. $)
  cprehil $a class PreHil $.

  $( Extend class notation with norm of a vector. $)
  cnormv $a class normv $.

  $( Extend class notation with orthocomplement of a subspace. $)
  cocv $a class ocv $.

  $( Extend class notation with set of closed subspaces. $)
  ccsubsp $a class CSubSp $.

  $( Extend class notation with class of all Hilbert spaces. $)
  chs $a class Hil $.

  ${
    $d a b c e f g h i k m p q r s t u v w x y z $.
    $( Define group operation.  Def. 8.5.2, p. 214. $)
    df-plusg $a |- +g = ( g e. _V |-> ( g ` 2 ) ) $.

    $( Define class of all groups.  Def. 8.5.1, p. 213. $)
    df-grpNEW $a |- GrpNEW = Struct ( 2 , f , E. g E. p ( g = ( base ` f )
       /\ p = ( +g ` f ) /\ ( A. a e. g A. b e. g A. c e. g ( ( a p b ) e. g
            /\ ( ( a p b ) p c ) = ( a p ( b p c ) ) ) /\
          E. e e. g A. a e. g ( ( e p a ) = a /\
            E. m e. g ( m p a ) = e ) ) ) ) $.

    $( Define group identity element.  Def. 8.5.4, p. 215. $)
    df-0g $a |- 0g = ( g e. _V |-> ( iota e ( e e. ( base ` g ) /\
      A. x e. ( base ` g ) ( e ( +g ` g ) x ) = x ) ) ) $.

    $( Define inverse of group element.  Def. 8.5.4, p. 215. $)
    df-minusg $a |- -g = ( g e. _V |-> ( x e. ( base ` g ) |-> ( iota w
          ( w e. ( base ` g ) /\ ( w ( +g ` g ) x ) = ( 0g ` g ) ) ) ) ) $.

    $( Define class of all Abelian groups.  Def. 8.5.5, p. 215. $)
    df-abel $a |- AbelNEW = { g e. GrpNEW | A. a e. ( base ` g )
      A. b e. ( base ` g ) ( a ( +g ` g ) b ) = ( b ( +g ` g ) a ) } $.

    $( Define ring multiplication.  Def. 8.5.7, p. 216. $)
    df-mulr $a |- .r = ( r e. _V |-> ( r ` 3 ) ) $.

    $( Define class of all rings (with unit).  Def. 8.5.6, p. 216. $)
    df-ringNEW $a |- RingNEW = Struct ( 3 , f , ( f e. AbelNEW /\
        E. r E. p E. t ( ( r = ( base ` f ) /\ p = ( +g ` f )
        /\ t = ( .r ` f ) ) /\ A. x e. r A. y e. r A. z e. r
           ( ( x t y ) e. r /\ ( ( ( x t y ) t z ) = ( x t ( y t z ) )
            /\ ( x t ( y p z ) ) = ( ( x t y ) p ( x t z ) )
            /\ ( ( x p y ) t z ) = ( ( x t z ) p ( y t z ) ) ) )
        /\ E. u e. r A. x e. r ( ( x t u ) = x /\ ( u t x ) = x ) ) ) ) $.

    $( Define ring unit.  Def. 8.5.7, p. 216. $)
    df-ur $a |- 1rNEW = ( r e. _V |-> ( iota u ( u e. ( base ` r ) /\
      A. x e. ( base ` r ) ( ( x ( .r ` r ) u ) = x
           /\ ( u ( .r ` r ) x ) = x ) ) ) ) $.

    $( Define ` s ` is a subring of ` r ` .  Def. 8.5.8, p. 216. $)
    df-subring $a |- C_rng = { <. s , r >. | ( ( base ` s ) C_ ( base ` r )
        /\ A. x e. ( base ` s ) A. y e. ( base ` s ) ( ( x ( +g ` s ) y )
  = ( x ( +g ` r ) y ) /\ ( x ( .r ` s ) y ) = ( x ( .r ` r ) y ) ) ) } $.

    $( Define class of all division rings.  Def. 8.5.9, p. 217. $)
    df-divring $a |- DivRingNEW = { r e. RingNEW |
      { <. 1 , ( ( base ` r ) \ ( 0g ` r ) ) >. , <. 2 , ( .r ` r ) >. }
        e. GrpNEW } $.

    $( Define multiplicative inverse.  Def. 8.5.10, p. 217. $)
    df-invr $a |- invr = ( r e. _V |-> ( -g `
      { <. 1 , ( ( base ` r ) \ ( 0g ` r ) ) >. , <. 2 , ( .r ` r ) >. } ) ) $.

    $( Define set of vectors in a vector space ` v ` .  Def. 8.5.12, p. 219. $)
    df-vbase $a |- vbase = ( v e. _V |-> ( v ` 4 ) ) $.

    $( Define vector addition.  Def. 8.5.12, p. 219. $)
    df-vadd $a |- vadd = ( v e. _V |-> ( v ` 5 ) ) $.

    $( Define scalar product.  Def. 8.5.12, p. 219. $)
    df-vsca $a |- vsca = ( v e. _V |-> ( v ` 6 ) ) $.

    $( Define class of all left vector spaces.  Def. 8.5.11, p. 218. $)
    df-lvec $a |- LVec = Struct ( 6 , f , ( f e. DivRing /\
      E. k E. p E. t E. v E. a E. s
         ( ( k = ( base ` f ) /\ p = ( +g ` f ) /\ t = ( .r ` f ) )
          /\ ( v = ( vbase ` f ) /\ a = ( vadd ` f ) /\ s = ( vsca ` f ) )
          /\ ( { <. 1 , v >. , <. 2 , a >. } e. AbelNEW
             /\ A. q e. k A. r e. k A. x e. v A. w e. v
               ( ( ( r s w ) e. v
                 /\ ( r s ( w a x ) ) = ( ( r s w ) a ( r s x ) )
                 /\ ( ( q p r ) s w ) = ( ( q s w ) a ( r s w ) )
               ) /\ ( ( q s ( r s w ) ) = ( ( q t r ) s w )
                 /\ ( ( 1rNEW ` f ) s w ) = w ) ) ) ) ) ) $.

    $( Define zero vector.  Def. 8.5.13, p. 219. $)
    df-zv $a |- 0vNEW = ( v e. _V |-> ( 0g `
             { <. 1 , ( vbase ` v ) >. , <. 2 , ( vadd ` v ) >. } ) ) $.

    $( Define vector subtraction.  Def. 8.5.14, p. 220. $)
    df-minusv $a |- -vNEW = ( v e. _V |-> ( x e. ( vbase ` v ) ,
         y e. ( vbase ` v ) |-> ( x ( vadd ` v )
 ( ( -g ` { <. 1 , ( vbase ` v ) >. , <. 2 , ( vadd ` v ) >. } ) ` y ) ) ) ) $.

    $( Define class of all right vector spaces.  Def. 8.5.15, p. 220. $)
    df-rvec $a |- RVecNEW = Struct ( 6 , f , ( f e. DivRing /\
      E. k E. p E. t E. v E. a E. s (
         ( k = ( base ` f ) /\ p = ( +g ` f ) /\ t = ( .r ` f ) )
          /\ ( v = ( vbase ` f ) /\ a = ( vadd ` f ) /\ s = ( vsca ` f ) )
          /\ ( { <. 1 , v >. , <. 2 , a >. } e. AbelNEW
             /\ A. q e. k A. r e. k A. x e. v A. w e. v
               ( ( ( w s r ) e. v
                 /\ ( ( w a x ) s r ) = ( ( w s r ) a ( x s r ) )
                 /\ ( w s ( q p r ) ) = ( ( w s q ) a ( w s r ) )
               ) /\ ( ( ( w s q ) s r ) = ( w s ( q t r ) )
                 /\ ( w s ( 1rNEW ` f ) ) = w ) ) ) ) ) ) $.

    $( Define subspace sum.  Def. 8.5.16, p. 221. $)
    df-plusss $a |- +ss = ( v e. _V |-> ( x e. ~P ( vbase ` v ) ,
        y e. ~P ( vbase ` v ) |-> { w e. ( vbase ` v )
            | E. t e. x E. u e. y w = ( t ( vadd ` v ) u ) } ) ) $.

    $( Define set of all subspaces of left vector space ` v ` .  Def. 8.5.17,
       p. 222. $)
    df-sbsp $a |- SubSpNEW = ( v e. _V |-> { s e. ~P ( vbase ` v )
      | ( ( 0vNEW ` v ) e. s /\ A. x e. s A. y e. s ( x ( vadd ` v ) y ) e. s
          /\ A. x e. ( base ` v ) A. y e. s ( x ( vsca ` v ) y ) e. s ) } ) $.

    $( Define span of a set of vectors.  Def. 8.5.18, p. 244. $)
    df-vspan $a |- spanNEW = ( v e. _V |-> ( m e. ~P ( vbase ` v ) |->
       |^| { s e. ( SubSpNEW ` v ) | m C_ s } ) ) $.

    $( Define direct sum of two left vectors spaces.  Def. 8.5.19, p. 223. $)
    df-oplus $a |- o+ = ( v e. _V , w e. _V |-> ( { <. 1 , ( base ` v ) >. ,
       <. 2 , ( +g ` v ) >. , <. 3 , ( .r ` v ) >. } u. {
       <. 4 , ( ( vbase ` v ) X. ( vbase ` w ) ) >. ,
       <. 5 , ( x e. ( ( vbase ` v ) X. ( vbase ` w ) ) ,
                y e. ( ( vbase ` v ) X. ( vbase ` w ) ) |->
                  <. ( ( 1st ` x ) ( vadd ` v ) ( 1st ` y ) ) ,
                     ( ( 2nd ` x ) ( vadd ` w ) ( 2nd ` y ) ) >. ) >. ,
       <. 6 , ( k e. ( base ` v ) ,
                x e. ( ( vbase ` v ) X. ( vbase ` w ) ) |->
                  <. ( k ( vsca ` v ) ( 1st ` x ) ) ,
                     ( k ( vsca ` w ) ( 2nd ` x ) ) >. ) >. } ) ) $.

    $( Define vector equivalence mod ` m ` .  Def. 8.5.20, p. 223. $)
    df-ev $a |- Ev = ( v e. _V , m e. _V |-> { <. x , y >. |
       ( x e. ( vbase ` v ) /\ y e. ( vbase ` v ) /\
       ( x ( -vNEW ` v ) y ) e. ( vbase ` m ) ) } ) $.

    $( Define quotient space.  Def. 8.5.21, p. 223. $)
    df-quov $a |- /v = ( v e. _V , m e. _V |-> ( { <. 1 , ( base ` v ) >. ,
       <. 2 , ( +g ` v ) >. , <. 3 , ( .r ` v ) >. } u. {
       <. 4 , ( ( vbase ` v ) /. ( v Ev m ) ) >. ,
       <. 5 , { <. <. a , b >. , c >. | E. x e. ( vbase ` v )
          E. y e. ( vbase ` v ) ( a = [ x ] ( v Ev m )
            /\ b = [ y ] ( v Ev m )
            /\ c = [ ( x ( vadd ` v ) y ) ] ( v Ev m ) ) } >. ,
       <. 6 , { <. <. a , b >. , c >. | ( a e. ( base ` v ) /\
          E. y e. ( vbase ` v ) ( b = [ y ] ( v Ev m )
            /\ c = [ ( x ( vsca ` v ) y ) ] ( v Ev m ) ) ) } >. } ) ) $.

    $( Define involution (the star operation of a star field).  Def. 8.5.23,
       p. 225. $)
    df-starv $a |- *v = ( v e. _V |-> ( v ` 7 ) ) $.

    $( Define class of all left vector spaces over *-fields (star fields).
       Def. 8.5.22, p. 224. $)
    df-lvecstar $a |- LVec* = Struct ( 7 , f , ( f e. LVec /\
       E. k E. p E. t E. i ( ( k = ( base ` f ) /\ p = ( +g ` f )
          /\ t = ( .r ` f ) ) /\ ( i = ( *v ` f ) /\
         A. q e. k A. r e. k ( ( ( i ` q ) e. k
              /\ ( i ` ( q p r ) ) = ( ( i ` q ) p ( i ` r ) )
         ) /\ ( ( i ` ( q t r ) ) = ( ( i ` r ) t ( i ` q ) )
           /\ ( i ` ( i ` q ) ) = q ) ) ) ) ) ) $.

    $( Define Hermitian form (inner product).  Def. 8.5.25, p. 226. $)
    df-inprod $a |- ip = ( v e. _V |-> ( v ` 8 ) ) $.

    $( Define class all pre-Hilbert spaces.  Def. 8.5.24, p. 225. $)
    df-prehil $a |- PreHil = Struct ( 8 , f , ( f e. LVec* /\
       E. k E. p E. t E. v E. a E. s E. i E. h (
         ( k = ( base ` f ) /\ p = ( +g ` f ) /\ t = ( .r ` f ) )
      /\ ( v = ( vbase ` f ) /\ a = ( vadd ` f ) /\ i = ( *v ` f ) )
      /\ ( h = ( ip ` f )
          /\ A. q e. k A. r e. k A. x e. v A. y e. v A. z e. v (
            ( ( x h y ) e. k
              /\ ( ( ( q s x ) a ( r s y ) ) h z )
                  = ( ( q t ( x h z ) ) p ( r t ( y h z ) ) )
              /\ ( x h ( ( q s y ) a ( r s z ) ) )
                  = ( ( ( x h y ) t ( i ` q ) ) p ( ( x h z ) t ( i ` r ) ) )
       ) /\ ( ( A. w e. v ( x h w ) = ( 0g ` f )
                \/ A. w e. v ( w h x ) = ( 0g ` f ) ) -> x = ( 0vNEW ` f ) )
              /\ ( i ` ( x h y ) ) = ( y h x ) ) ) ) ) ) $.

    $( Define norm of a vector.  Def. 8.5.26, p. 226. $)
    df-normv $a |- normv = ( h e. _V |-> ( v ( ip ` h ) v ) ) $.

    $( Define orthocomplement of a subspace.  Def. 8.5.27, p. 227. $)
    df-ocv $a |- ocv = ( h e. _V |-> ( s e. ~P ( vbase ` h ) |->
   { x e. ( vbase ` h ) | A. y e. s ( x ( ip ` h ) y ) = ( 0vNEW ` h ) } ) ) $.

    $( Define set of closed subspaces.  Def. 8.5.28, p. 226. $)
    df-csubsp $a |- CSubSp = ( h e. _V |-> { s e. ~P ( vbase ` h )
        | s = ( ( oc ` h ) ` ( ( oc ` h ) ` s ) ) } ) $.

    $( Define class of all Hilbert spaces.  Def. 8.5.30, p. 228. $)
    df-hil $a |- Hil = { h e. PreHil | A. s e. ( CSubSp ` h )
          ( s ( +ss ` h ) ( ( oc ` h ) ` s ) ) = ( vbase ` h ) } $.
  $}

  ${
    $d a b c e f g m p B $.  $d f g n p G $.  $d a b c e f g m p P $.
    $d a b c e m n $.
    isgrp.1NEW $e |- B = ( base ` G ) $.
    isgrp.2NEW $e |- P = ( +g ` G ) $.
    $( The predicate "is a group operation." $)
    isgrpNEW $p |- ( G e. GrpNEW <-> ( E. n e. NN ( 2 <_ n /\ G
      Fn ( 1 ... n ) ) /\ A. a e. B A. b e. B A. c e. B
            ( ( a P b ) e. B /\ ( ( a P b ) P c ) = ( a P ( b P c ) ) )
     /\ E. e e. B A. a e. B ( ( e P a ) = a /\ E. m e. B ( m P a ) = e ) ) ) $=
      ( vg vf vp cgrp wcel cv cbs cfv wceq cplusg co wa wral wrex w3a wex c2
      cstru cle wbr c1 cfz wfn cn df-grpNEW eleq2i fveq2 syl6eqr eqeq2d
      3anbi12d 2exbidv elstr cvv fvex eqeltri eleq2 anbi1d raleqd rexeq1
      anbi2d rexeqd anbi12d opreq eleq1d opreq1d eqtrd opreq2d eqeq12d ralbidv
      2ralbidv eqeq1d rexbidv rexralbidv ceqsex2v anbi2i 3anass bitr4i 3bitri
      ) FOPFLQZMQZRSZTZNQZWKUASZTZGQZHQZWNUBZWJPZWSIQZWNUBZWQWRXAWNUBZWNUBZTZUC
      ZIWJUDZHWJUDZGWJUDZCQZWQWNUBZWQTZDQZWQWNUBZXJTZDWJUEZUCZGWJUDZCWJUEZUCZUF
      ZNUGLUGZMUHUIZPUHEQZUJUKFULYDUMUBUNUCEUOUEZWJATZWNBTZXTUFZNUGLUGZUCZYEWQW
      RBUBZAPZYKXABUBZWQWRXABUBZBUBZTZUCZIAUDZHAUDGAUDZXJWQBUBZWQTZXMWQBUBZXJTZ
      DAUEZUCZGAUDCAUEZUFZOYCFCMLDNGHIUPUQYBYIMEFUHWKFTZYAYHLNUUHWMYFWPYGXTUUHW
      LAWJUUHWLFRSZAWKFRURJUSUTUUHWOBWNUUHWOFUASZBWKFUAURKUSUTVAVBVCYJYEYSUUFUC
      ZUCUUGYIUUKYEXTWSAPZXEUCZIAUDZHAUDZGAUDZXLXODAUEZUCZGAUDZCAUEZUCUUKLNABAU
      UIVDJFRVEVFBUUJVDKFUAVEVFYFXIUUPXSUUTXHUUOGWJAXGUUNHWJAXFUUMIWJAYFWTUULXE
      WJAWSVGVHVIVIVIXRUUSCWJAXQUURGWJAYFXPUUQXLXODWJAVJVKVIVLVMYGUUPYSUUTUUFYG
      UUNYRGHAAYGUUMYQIAYGUULYLXEYPYGWSYKAWQWRWNBVNZVOYGXBYMXDYOYGXBWSXABUBYMWS
      XAWNBVNYGWSYKXABUVAVPVQYGXDWQXCBUBYOWQXCWNBVNYGXCYNWQBWRXAWNBVNVRVQVSVMVT
      WAYGUURUUECGAAYGXLUUAUUQUUDYGXKYTWQXJWQWNBVNWBYGXOUUCDAYGXNUUBXJXMWQWNBVN
      WBWCVMWDVMWEWFYEYSUUFWGWHWI $.
      $( [13-Aug-2011] $) $( [13-Aug-2011] $)

    $d a b c X $.  $d b c Y $.  $d c Z $.
    $( Lemma for group properties. $)
    grplem1 $p |- ( ( G e. GrpNEW /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
       ( ( X P Y ) e. B /\ ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) ) $=
      ( va vb vc vn ve vm cgrp wcel cv co wceq wa wral w3a c2 cle wbr c1 cfz
      wfn cn wrex isgrpNEW 3simp2 sylbi opreq1 eleq1d opreq1d eqeq12d anbi12d
      opreq2 opreq2d anbi2d rcla43v mpan9 ) COPZIQZJQZBRZAPZVGKQZBRZVEVFVIBRZBR
      ZSZTZKAUAJAUAIAUAZDAPEAPFAPUBDEBRZAPZVPFBRZDEFBRZBRZSZTZVDUCLQZUDUECUFWCU
      GRUHTLUIUJZVOMQZVEBRVESNQVEBRWESNAUJTIAUAMAUJZUBVOABMNLCIJKGHUKWDVOWFULUM
      VNWBDVFBRZAPZWGVIBRZDVKBRZSZTVQVPVIBRZDEVIBRZBRZSZTIJKDEFAAAVEDSZVHWHVMWK
      WPVGWGAVEDVFBUNZUOWPVJWIVLWJWPVGWGVIBWQUPVEDVKBUNUQURVFESZWHVQWKWOWRWGVPA
      VFEDBUSZUOWRWIWLWJWNWRWGVPVIBWSUPWRVKWMDBVFEVIBUNUTUQURVIFSZWOWAVQWTWLVRW
      NVTVIFVPBUSWTWMVSDBVIFEBUSUTUQVAVBVC $.
      $( [14-Aug-2011] $) $( [14-Aug-2011] $)

    $( Closure of the operation of a group. $)
    grpclNEW $p |- ( ( G e. GrpNEW /\ X e. B /\ Y e. B ) -> ( X P Y ) e. B ) $=
      ( cgrp wcel co w3a wa wceq grplem1 pm3.26d 3anass anidm anbi2i bitri
      sylan2br 3impb ) CHIZDAIZEAIZDEBJZAIZUBUCUDUDKZUFUCUDLZUBUGLUFUEEBJDEEBJB
      JMABCDEEFGNOUGUCUDUDLZLUHUCUDUDPUIUDUCUDQRSTUA $.
      $( [14-Aug-2011] $) $( [14-Aug-2011] $)

    $( A group operation is associative. $)
    grpassNEW $p |- ( ( G e. GrpNEW /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
              ( ( X P Y ) P Z ) = ( X P ( Y P Z ) ) ) $=
      ( cgrp wcel w3a wa co wceq grplem1 pm3.27d ) CIJDAJEAJFAJKLDEBMZAJQFBMDEF
      BMBMNABCDEFGHOP $.
      $( [14-Aug-2011] $) $( [14-Aug-2011] $)

    $d u x y B $.  $d u x y P $.  $d a b n u x y $.
    $( A group has a left identity element, and every member has a left
       inverse. $)
    grplidinvNEW $p |- ( G e. GrpNEW ->
         E. u e. B A. x e. B ( ( u P x ) = x /\ E. y e. B ( y P x ) = u ) ) $=
      ( vn vb va cgrp wcel c2 cv cle wbr c1 cfz co wfn wa cn wrex wceq wral
      w3a isgrpNEW biimpi 3simp3d ) FLMZNIOZPQFRULSTUAUBIUCUDZAOZJOZETZDMUPKOZE
      TUNUOUQETETUEUBKDUFJDUFADUFZCOZUNETUNUEBOUNETUSUEBDUDUBADUFCDUDZUKUMURUTU
      GDECBIFAJKGHUHUIUJ $.
      $( [16-Aug-2011] $) $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinvNEW . $)
    grpidinvlem1NEW $p |- ( ( ( G e. GrpNEW /\ ( Y e. B /\ A e. B ) ) /\
                  ( ( Y P A ) = U /\ ( A P A ) = A ) ) -> ( U P A ) = U ) $=
      ( cgrp wcel wa co wceq opreq1 ad2antrl w3a grpassNEW id 3anidm23 sylan2
      adantr eqtr3d opreq2 ad2antll simprl 3eqtrd ) EIJZFBJZABJZKZKZFACLZDMZAAC
      LZAMZKZKZDACLZFUNCLZULDUQULACLZURUSUMUTURMUKUOULDACNOUKUTUSMZUPUGUHUIUIPZ
      VAUJBCEFAAGHQUHUIVBVBRSTUAUBUOUSULMUKUMUNAFCUCUDUKUMUOUEUF $.
      $( [?] $) $( [16-Aug-2011] $)

    $( Lemma for ~ grpidinvNEW . $)
    grpidinvlem2NEW $p |- ( ( ( G e. GrpNEW /\ ( Y e. B /\ A e. B ) ) /\ ( (
 U P Y ) = Y /\ ( Y P A ) = U ) ) -> ( ( A P Y ) P ( A P Y ) ) = ( A P Y ) ) $=
      ( cgrp wcel wa co wceq w3a simprr simprl grpclNEW 3com23 3expb 3jca
      grpassNEW syldan adantr opreq1 adantl pm3.26 eqtr2d id 3anidm13 sylan2
      sylan9eqr eqcomd opreq2d eqtrd ) EIJZFBJZABJZKZKZDFCLZFMZFACLZDMZKZKZAFCL
      ZVFCLZAFVFCLZCLZVFUSVGVIMZVDUOURUQUPVFBJZNVJUSUQUPVKUOUPUQOUOUPUQPUOUPUQV
      KUOUQUPVKBCEAFGHQRSTBCEAFVFGHUAUBUCVEVHFACVEFVHVDUSFVBFCLZVHVDVLUTFVCVLUT
      MVAVBDFCUDUEVAVCUFUGUOUPUQUPNZVLVHMURBCEFAFGHUAUPUQVMVMUHUIUJUKULUMUN $.
      $( [?] $) $( [16-Aug-2011] $)

    ${
      $d w x y A $.  $d w z B $.  $d w y G $.  $d w z P $.  $d w x y z U $.
      $d w y ph $.  $d y ps $.
      grpidinvlem3.2NEW $e |- ( ph <-> A. x e. B ( U P x ) = x ) $.
      grpidinvlem3.3NEW $e |- ( ps <-> A. x e. B E. z e. B ( z P x ) = U ) $.
      $( Lemma for ~ grpidinvNEW . $)
      grpidinvlem3NEW $p |- ( ( ( ( G e. GrpNEW /\ U e. B ) /\ ( ph /\ ps ) )
               /\ A e. B ) -> E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) $=
        ( vw cgrp wcel wa cv co wceq wrex wral opreq2 eqeq1d rexbidv rcla4cva
        opreq1 cbvrexv ralbii bitri sylanb adantll id eqeq12d rcla4va grpclNEW
        3expa adantllr biimpi ad2antrl ad2antrr sylanc adantr grpidinvlem2NEW
        pm3.22 ancom31s adantlr adantlll anim1i wi 3expb ad2ant2rl
        grpidinvlem1NEW anass an1rs ex syldan imp sylan exp43 r19.23adv
        sylan2b syl5 mpand exp32 com34 imp32 exp3a imp31 mpd eqtr3d ancld
        r19.22dva ) JPQZIGQZRZABRZRZFGQZRZDSZFHTZIUAZDGUBZXDFXBHTZIUAZRZDGUBWRW
        TXEWQBWTXEAXBCSZHTZIUAZDGUBZCGUCZWTXEBXLXECFGXIFUAZXKXDDGXNXJXCIXIFXBHU
        DUEUFUGBESZXIHTZIUAZEGUBZCGUCZXMNXRXLCGXQXKEDGXOXBUAXPXJIXOXBXIHUHUEUIU
        JUKULUMUMXAXDXHDGXAXBGQZRZXDXGYAXDXGYAXDRZIXFHTZXFIYAYCXFUAZXDXFGQZIXIH
        TZXIUAZCGUCZYDYAYGYDCXFGXIXFUAZYFYCXIXFXIXFIHUDYIUNUOUPWQWTXTYEWRWOWTXT
        YEWPWOWTXTYEGHJFXBKLUQZURUSUSWSYHWTXTAYHWQBAYHMUTVAVBVCVDYBXFXFHTXFUAZY
        CIUAZWOXTWTRZRZIXBHTZXBUAZXDRYKYBFGHIJXBKLVEYAYNXDWQWTXTYNWRWOWTXTYNWPX
        TWTWOYNYMWOVFVGUSUSVDYAYPXDWRWTXTYPWQWRXTYPWTAXTYPBYHXTYPAYGYPCXBGXIXBU
        AZYFYOXIXBXIXBIHUDYQUNUOUGMULVHVHVIVJVCYAYKYLVKZXDWSWTXTYRWSWTXTYRWQABW
        TXTRZYRVKWQAYSBYRWQAYSBYRVKWQAYSRRZYEBYRWOYSYEWPAWOWTXTYEYJVLZVMYTOSZXF
        HTZIUAZOGUBZYRYEBRYTUUDYROGYTUUBGQZUUDYKYLWOUUFYERRZUUDYKRYLYTUUFRXFGHI
        JUUBKLVNYTUUFUUGWOYSUUFUUGVKZWPAWOYSYEUUHUUAWOYERUUFUUGWOUUFYEUUGWOUUFR
        YERUUGWOUUFYEVOUTVPVQVRVMVSVTWAWBYEUUBXIHTZIUAZOGUBZCGUCZUUEBUUKUUECXFG
        YIUUJUUDOGYIUUIUUCIXIXFUUBHUDUEUFUPBXSUULNXRUUKCGXQUUJEOGXOUUBUAXPUUIIX
        OUUBXIHUHUEUIUJUKWCWDWEWFWGWHWIWJVDWKWLVQWMWNWK $.
        $( [?] $) $( [16-Aug-2011] $)
    $}

    $d y G $.  $d y U $.  $d y X $.
    $( Lemma for ~ grpidinvNEW . $)
    grpidinvlem4NEW $p |- ( ( ( G e. GrpNEW /\ X e. B ) /\ E. y e. B
             ( ( y P X ) = U /\ ( X P y ) = U ) ) -> ( X P U ) = ( U P X ) ) $=
      ( cgrp wcel wa cv co wceq wrex w3a grpassNEW simpll simplr pm3.27 3jca
      sylanc opreq2 sylan9eq opreq1 sylan9req anasss exp31 r19.23adv imp ) EIJZ
      FBJZKZALZFCMZDNZFUNCMZDNZKZABOFDCMZDFCMZNZUMUSVBABUMUNBJZUSVBUMVCKZUPURVB
      VDUPKURUTUQFCMZVAVDUPVEFUOCMZUTUKULVCULPVEVFNVDBCEFUNFGHQUKULVCRVDULVCULU
      KULVCSZUMVCTVGUAUBUODFCUCUDUQDFCUEUFUGUHUIUJ $.
      $( [?] $) $( [16-Aug-2011] $)

    $d w z B $.  $d u x G $.  $d w z P $.  $d u w x y z $.
    $( A group has a left and right identity element, and every member has a
       left and right inverse. $)
    grpidinvNEW $p |- ( G e. GrpNEW -> E. u e. B A. x e. B ( ( ( u P x ) = x /\
         ( x P u ) = x ) /\ E. y e. B ( ( y P x ) = u /\ ( x P y ) = u ) ) ) $=
      ( vz vw cgrp wcel cv co wceq wrex wa wral grplidinvNEW opreq2 id eqeq12d
      rcla4cva pm3.26 r19.20si sylan adantll grpidinvlem4NEW anim1i adantrr
      adantr adantl ad2antlr pm3.27 jca32 biid grpidinvlem3NEW sylancom sylanc
      eqtrd jca31 r19.21aiva exp32 r19.22dv mpd ) FKLZCMZIMZENZVHOZJMVHENVGOJDP
      ZQZIDRZCDPVGAMZENZVNOZVNVGENZVNOZQBMZVNENVGOVNVSENVGOQBDPZQZADRZCDPIJCDEF
      GHSVFVMWBCDVFVGDLZVMWBVFWCVMQZQZWAADWEVNDLZQZVPVRVTWDWFVPVFVMWFVPWCVJIDRZ
      WFVPVMVJVPIVNDVHVNOZVIVOVHVNVHVNVGETWIUAUBUCVLVJIDVJVKUDUEZUFUGUGZWGVQVOV
      NVFWFQVTVQVOOWGBDEVGFVNGHUHWEVFWFVFWDUDUIWEWFVFWCQZWHVKIDRZQQVTWGWLWHWMWE
      WLWFVFWCWLVMWLUAUJUKWDWHVFWFVMWHWCWJULUMWDWMVFWFVMWMWCVLVKIDVJVKUNUEULUMU
      OWHWMIBJVNDEVGFGHWHUPWMUPUQURZUSWKUTWNVAVBVCVDVE $.
      $( [?] $) $( [16-Aug-2011] $)

    $d w G $.  $d z G $.
    $( The left identity element of a group is unique.  Lemma 2.2.1(a) of
       [Herstein] p. 55. $)
    grpideuNEW $p |- ( G e. GrpNEW -> E! u e. B A. x e. B ( u P x ) = x ) $=
      ( vw vz vy cgrp wcel cv co wceq wral wi wa wrex wreu grpidinvNEW simpll
      r19.20si opreq2 id eqeq12d cbvralv sylib adantl ad2antlr eqeq1d opreq1
      anbi12d rexbidv rcla4va adantll pm3.27 sylan2 grpidinvlem4NEW syldan
      an1rs adantllr adantr ad2ant2rl ad2ant2lr 3eqtr3d ex mpand r19.21aiva
      jca r19.22dva mpd ralbidv reu8 sylibr ) EKLZBMZAMZDNZVROZACPZHMZVRDNZVROZ
      ACPZVQWBOZQZHCPZRZBCSZWABCTVPVQIMZDNZWKOZWKVQDNWKOZRZJMZWKDNZVQOZWKWPDNZV
      QOZRZJCSZRZICPZBCSWJIJBCDEFGUAVPXDWIBCVPVQCLZRZXDWIXFXDRZWAWHXDWAXFXDWMIC
      PWAXCWMICWMWNXBUBUCWMVTIACWKVROZWLVSWKVRWKVRVQDUDXHUEUFUGUHZUIXGWGHCXGWBC
      LZRZWAWEWFXDWAXFXJXIUJXKWAWERZWFXKXLRWBVQDNZVQWBDNZVQWBXKXMXNOZXLVPXDXJXO
      XEVPXJXDXOVPXJRZXDWPWBDNZVQOZWBWPDNZVQOZRZJCSZXOXPXBICPZYBXDXJYCYBVPXBYBI
      WBCWKWBOZXAYAJCYDWRXRWTXTYDWQXQVQWKWBWPDUDUKYDWSXSVQWKWBWPDULUKUMUNUOUPXC
      XBICWOXBUQUCURJCDVQEWBFGUSUTVAVBVCXFXJXLXMVQOZXDXFWEYEXJWAXEWEYEVPWDYEAVQ
      CVRVQOZWCXMVRVQVRVQWBDUDYFUEUFUOUPVDVBXJWAXNWBOZXGWEVTYGAWBCVRWBOZVSXNVRW
      BVRWBVQDUDYHUEUFUOVEVFVGVHVIVJVGVKVLWAWEBHCWFVTWDACWFVSWCVRVQWBVRDULUKVMV
      NVO $.
      $( [?] $) $( [16-Aug-2011] $)
  $}

  ${
    $d x y A $.  $d e g u x y G $.  $d u x y U $.  $d g u x y X $.
    $d g u x y B $.  $d g u x y P $.
    grpidval.1NEW $e |- B = ( base ` G ) $.
    grpidval.2NEW $e |- P = ( +g ` G ) $.
    grpidval.3NEW $e |- U = ( 0g ` G ) $.
    $( The value of the identity element of a group. $)
    grpidvalNEW $p |- ( G e. A -> U
              = ( iota e ( e e. B /\ A. x e. B ( e P x ) = x ) ) ) $=
      ( vg wcel cvv cv co wceq wral wa ciota elisset c0g cfv cbs cplusg fveq2
      syl6eqr eleq2d opreqd eqeq1d raleqbidv anbi12d iotabidv df-0g iotaex
      fvmpt syl5eq syl ) GBLGMLZEFNZCLZUSANZDOZVAPZACQZRZFSZPGBTURGUAUBVFEKGUSK
      NZUCUBZLZUSVAVGUDUBZOZVAPZAVHQZRZFSVFMUAVGGPZVNVEFVOVIUTVMVDVOVHCUSVOVHGU
      CUBCVGGUCUEHUFZUGVOVLVCAVHCVPVOVKVBVAVOVJDUSVAVOVJGUDUBDVGGUDUEIUFUHUIUJU
      KULAFKUMVEFUNUOJUPUQ $.
      $( [20-Aug-2011] $) $( [20-Aug-2011] $)

    $( The identity element of a group belongs to the group. $)
    grpidclNEW $p |- ( G e. GrpNEW -> U e. B ) $=
      ( vu vx cgrp wcel cv co wceq wral wreu grpideuNEW wa ciota grpidvalNEW
      adantr crab riotacl2 ssrab2 sseli syl adantl eqeltrd mpdan ) DJKZHLZILZBM
      ULNIAOZHAPZCAKIHABDEFQUJUNRCUKAKUMRHSZAUJCUONUNIJABCHDEFGTUAUNUOAKZUJUNUO
      UMHAUBZKUPUMHAUCUQAUOUMHAUDUEUFUGUHUI $.
      $( [?] $) $( [18-Aug-2011] $)

    $( A group's properties using the explicit identity element. $)
    grpidinv2NEW $p |- ( ( G e. GrpNEW /\ A e. B ) -> ( ( ( U P A ) = A /\
         ( A P U ) = A ) /\ E. y e. B ( ( y P A ) = U /\ ( A P y ) = U ) ) ) $=
      ( vx vu cv co wceq wa wrex wral wcel cgrp opreq2 id eqeq12d opreq1
      anbi12d eqeq1d rexbidv rcla4cva ciota wreu wss wi ssid simpll r19.20si
      a1i rgen riotass2 mpanl12 grpidinvNEW grpideuNEW sylanc grpidvalNEW
      eqtr4d wb eqeq2 ralbidv riota2 grpidclNEW reuss2 mpbird sylan ) EJLZDMZVL
      NZVLEDMZVLNZOZALZVLDMZENZVLVRDMZENZOZACPZOZJCQZBCREBDMZBNZBEDMZBNZOZVRBDM
      ZENZBVRDMZENZOZACPZOZFSRZWEWRJBCVLBNZVQWKWDWQWTVNWHVPWJWTVMWGVLBVLBEDTWTU
      AZUBWTVOWIVLBVLBEDUCXAUBUDWTWCWPACWTVTWMWBWOWTVSWLEVLBVRDTUEWTWAWNEVLBVRD
      UCUEUDUFUDUGWSWFKLZCRZXBVLDMZVLNZVLXBDMZVLNZOZVSXBNZWAXBNZOZACPZOZJCQZOKU
      HZENZWSXOXCXEJCQZOKUHZEXNKCPZXQKCUIZXOXRNZWSCCUJZXNXQUKZKCQZXSXTOZYACULZY
      CKCYCXCXMXEJCXEXGXLUMUNUOUPZXNXQKCCUQURJAKCDFGHUSZJKCDFGHUTZVAJSCDEKFGHIV
      BVCECRXNKCUIZWFXPVDWSXNWFKCEXBENZXMWEJCYKXHVQXLWDYKXEVNXGVPYKXDVMVLXBEVLD
      UCUEYKXFVOVLXBEVLDTUEUDYKXKWCACYKXIVTXJWBXBEVSVEXBEWAVEUDUFUDVFVGCDEFGHIV
      HXSXTYJWSYBYDYEYJYFYGXNXQKCCVIURYHYIVAVAVJVK $.
      $( [?] $) $( [18-Aug-2011] $)

    $( The identity element of a group is a left identity. $)
    grplidNEW $p |- ( ( G e. GrpNEW /\ A e. B ) -> ( U P A ) = A ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2NEW simpll syl ) EJKABKLDACMAN
      ZADCMANZLIOZACMDNAUBCMDNLIBPZLTIABCDEFGHQTUAUCRS $.
      $( [?] $) $( [18-Aug-2011] $)

    $( The identity element of a group is a right identity. $)
    grpridNEW $p |- ( ( G e. GrpNEW /\ A e. B ) -> ( A P U ) = A ) $=
      ( vy cgrp wcel wa co wceq cv wrex grpidinv2NEW simplr syl ) EJKABKLDACMAN
      ZADCMANZLIOZACMDNAUBCMDNLIBPZLUAIABCDEFGHQTUAUCRS $.
      $( [?] $) $( [18-Aug-2011] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.  $d y Y $.  $d y Z $.
    grprcan.1NEW $e |- B = ( base ` G ) $.
    grprcan.2NEW $e |- P = ( +g ` G ) $.
    $( Right cancellation law for groups. $)
    grprcanNEW $p |- ( ( G e. GrpNEW /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( X P Z ) = ( Y P Z ) <-> X = Y ) ) $=
      ( vy cgrp wcel co wceq wb wa cv c0g cfv wrex wi eqid grpidinv2NEW pm3.27
      r19.22si adantl syl ad2ant2rl opreq1 ad2antll grpassNEW 3exp2 imp41
      adantlrl adantrr imp42 adantllr 3eqtr3d adantrrl opreq2 ad2antrl
      grpridNEW ad2antrr ad2ant2r adantr exp45 r19.23adv mpd impbid1 exp43
      3imp2 ) CJKZDAKZEAKZFAKZDFBLZEFBLZMZDEMZNZVKVLVMVNVSVKVLOZVMVNOZOZVQVRWBF
      IPZBLZCQRZMZIASZVQVRTZVKVNWGVLVMVKVNOWEFBLFMFWEBLFMOZWCFBLWEMZWFOZIASZOWG
      IFABWECGHWEUAZUBWLWGWIWKWFIAWJWFUCUDUEUFUGWBWFWHIAWBWCAKZWFVQVRWBWNWFVQOO
      ZOZDWEBLZEWEBLZDEWPDWDBLZEWDBLZWQWRWBWNVQWSWTMWFWBWNVQOOVOWCBLZVPWCBLZWSW
      TVQXAXBMWBWNVOVPWCBUHUIWBWNXAWSMZVQVTVNWNXCVMVKVLVNWNXCVKVLVNWNXCABCDFWCG
      HUJUKULUMUNWBWNXBWTMZVQVKWAWNXDVLVKVMVNWNXDVKVMVNWNXDABCEFWCGHUJUKUOUPUNU
      QURWOWSWQMZWBWFXEWNVQWDWEDBUSUTUEWOWTWRMZWBWFXFWNVQWDWEEBUSUTUEUQVTWQDMWA
      WODABWECGHWMVAVBWBWREMZWOVKVMXGVLVNEABWECGHWMVAVCVDUQVEVFVGDEFBUHVHVIVJ
      $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y z B $.  $d y z G $.  $d y z P $.  $d y z U $.  $d y z X $.
    grpinveu.1NEW $e |- B = ( base ` G ) $.
    grpinveu.2NEW $e |- P = ( +g ` G ) $.
    grpinveu.3NEW $e |- U = ( 0g ` G ) $.
    $( The left inverse element of a group is unique.  Lemma 2.2.1(b) of
       [Herstein] p. 55. $)
    grpinveuNEW $p |- ( ( G e. GrpNEW /\ X e. B )
           -> E! y e. B ( y P X ) = U ) $=
      ( vz cgrp wcel wa cv co wceq wi wral wrex wreu grpidinv2NEW pm3.26
      r19.22si adantl syl w3a grprcanNEW eqtr3 syl5bi 3exp2 com24 imp41 an1rs
      exp3a r19.21adva ancld r19.22dva mpd opreq1 eqeq1d reu8 sylibr ) EKLZFBLZ
      MZANZFCOZDPZJNZFCOZDPZVFVIPZQZJBRZMZABSZVHABTVEVHABSZVPVEDFCOFPFDCOFPMZVH
      FVFCODPZMZABSZMVQAFBCDEGHIUAWAVQVRVTVHABVHVSUBUCUDUEVEVHVOABVEVFBLZMZVHVN
      WCVHVMJBWCVIBLZMVHVKVLVEWDWBVHVKMZVLQZVCVDWDWBWFVCWBWDVDWFVCWBWDVDWFVCWBW
      DVDUFMVGVJPVLWEBCEVFVIFGHUGVGVJDUHUIUJUKULUMUNUOUPUQURVHVKAJBVLVGVJDVFVIF
      CUSUTVAVB $.
      $( [?] $) $( [24-Aug-2011] $)

    $( Two ways of saying that an element of a group is the identity
       element. $)
    grpidNEW $p |- ( ( G e. GrpNEW /\ X e. B ) ->
                  ( X = U <-> ( X P X ) = X ) ) $=
      ( cgrp wcel wa co wceq wb wi grpidclNEW grprcanNEW 3exp2 mpid pm2.43d
      imp grplidNEW eqeq2d bitr3d ) DIJZEAJZKZEEBLZCEBLZMZECMZUHEMUEUFUJUKNZUEU
      FULUEUFCAJZUFULOABCDFGHPUEUFUMUFULABDECEFGQRSTUAUGUIEUHEABCDFGHUBUCUD $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    $d f g n x y B $.  $d g x y G $.  $d f g n x y P $.  $d f g n x U $.
    $d n x y X $.  $d y Y $.  $d y Z $.
    grpinvfval.1NEW $e |- B = ( base ` G ) $.
    grpinvfval.2NEW $e |- P = ( +g ` G ) $.
    grpinvfval.3NEW $e |- U = ( 0g ` G ) $.
    grpinvfval.4NEW $e |- N = ( -g ` G ) $.
    $( The inverse function of a group. $)
    grpinvfvalNEW $p |- ( G e. A
              -> N = ( x e. B |-> ( iota y ( y e. B /\ ( y P x ) = U ) ) ) ) $=
      ( vg wcel cvv cv co wceq wa ciota cmpt elisset cminusg cfv cbs cplusg
      c0g fveq2 eqcomi syl6eq eleq2d syl6eqr opreqd eqeq12d anbi12d iotabidv
      mpteq12dv df-minusg fvex eqeltri mptexg ax-mp fvmpt syl5eq syl ) GCNGONZH
      ADBPZDNZVGAPZEQZFRZSZBTZUAZRGCUBVFGUCUDVNHMGAMPZUEUDZVGVPNZVGVIVOUFUDZQZV
      OUGUDZRZSZBTZUAVNOUCVOGRZAVPWCDVMWDVPGUEUDZDVOGUEUHDWEIUIUJZWDWBVLBWDVQVH
      WAVKWDVPDVGWFUKWDVSVJVTFWDVREVGVIWDVRGUFUDEVOGUFUHJULUMWDVTGUGUDFVOGUGUHK
      ULUNUOUPUQABMURDONVNONDWEOIGUEUSUTADOVMVAVBVCLVDVE $.
      $( [?] $) $( [24-Aug-2011] $)

    $( The inverse of a group element. $)
    grpinvvalNEW $p |- ( ( G e. A /\ X e. B ) ->
                    ( N ` X ) = ( iota y ( y e. B /\ ( y P X ) = U ) ) ) $=
      ( vx wcel cfv cv co wceq wa ciota cmpt grpinvfvalNEW fveq1d opreq2
      eqeq1d anbi2d iotabidv eqid iotaex fvmpt sylan9eq ) FBNZHCNHGOHMCAPZCNZUM
      MPZDQZERZSZATZUAZOUNUMHDQZERZSZATZULHGUTMABCDEFGIJKLUBUCMHUSVDCUTUOHRZURV
      CAVEUQVBUNVEUPVAEUOHUMDUDUEUFUGUTUHVCAUIUJUK $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y P $.  $d y X $.
    grpinvcl.1NEW $e |- B = ( base ` G ) $.
    grpinvcl.2NEW $e |- P = ( +g ` G ) $.
    grpinvcl.3NEW $e |- N = ( -g ` G ) $.
    $( X group element's inverse is a group element. $)
    grpinvclNEW $p |- ( ( G e. GrpNEW /\ X e. B ) -> ( N ` X ) e. B ) $=
      ( vy cgrp wcel wa cfv cv co c0g wceq ciota eqid grpinvvalNEW wreu
      grpinveuNEW riotacl syl eqeltrd ) CJKEAKLZEDMINZAKUGEBOCPMZQZLIRZAIJABUHC
      DEFGUHSZHTUFUIIAUAUJAKIABUHCEFGUKUBUIIAUCUDUE $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    $d y B $.  $d y G $.  $d y N $.  $d y P $.  $d y U $.  $d y X $.
    grpinv.1NEW $e |- B = ( base ` G ) $.
    grpinv.2NEW $e |- P = ( +g ` G ) $.
    grpinv.3NEW $e |- U = ( 0g ` G ) $.
    grpinv.4NEW $e |- N = ( -g ` G ) $.
    $( The properties of a group element's inverse. $)
    grpinvNEW $p |- ( ( G e. GrpNEW /\ X e. B ) ->
                 ( ( ( N ` X ) P X ) = U /\ ( X P ( N ` X ) ) = U ) ) $=
      ( vy cgrp wcel wa cfv co wceq cv ciota wrex wreu wss wi wral ssid pm3.26
      a1i rgen riotass2 mpanl12 grpidinv2NEW pm3.27d grpinveuNEW sylanc
      grpinvvalNEW eqtr4d wb opreq1 eqeq1d opreq2 anbi12d riota2 grpinvclNEW
      reuss2 mpbird ) DLMFAMNZFEOZFBPZCQZFVGBPZCQZNZKRZAMZVMFBPZCQZFVMBPZCQZNZN
      KSZVGQZVFVTVNVPNKSZVGVSKATZVPKAUAZVTWBQZVFAAUBZVSVPUCZKAUDZWCWDNZWEAUEZWG
      KAWGVNVPVRUFUGUHZVSVPKAAUIUJVFCFBPFQFCBPFQNWCKFABCDGHIUKULZKABCDFGHIUMZUN
      KLABCDEFGHIJUOUPVGAMVSKAUAZVLWAUQVFVSVLKAVGVMVGQZVPVIVRVKWOVOVHCVMVGFBURU
      SWOVQVJCVMVGFBUTUSVAVBABDEFGHJVCWCWDWNVFWFWHWIWNWJWKVSVPKAAVDUJWLWMUNUNVE
      $.
      $( [?] $) $( [24-Aug-2011] $)

    $( The left inverse of a group element. $)
    grplinvNEW $p |- ( ( G e. GrpNEW /\ X e. B ) -> ( ( N ` X ) P X ) = U ) $=
      ( cgrp wcel wa cfv co wceq grpinvNEW pm3.26d ) DKLFALMFENZFBOCPFSBOCPABCD
      EFGHIJQR $.
      $( [?] $) $( [24-Aug-2011] $)

    $( The right inverse of a group element. $)
    grprinvNEW $p |- ( ( G e. GrpNEW /\ X e. B ) -> ( X P ( N ` X ) ) = U ) $=
      ( cgrp wcel wa cfv co wceq grpinvNEW pm3.27d ) DKLFALMFENZFBOCPFSBOCPABCD
      EFGHIJQR $.
      $( [?] $) $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid1NEW $p |- ( ( G e. GrpNEW /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( X P Y ) = U ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq2 adantl grprinvNEW 3adant3 adantr
      eqtr3d grplinvNEW opreq1d grpinvclNEW adantrr simprl simprr 3jca
      grpassNEW syldan 3impb grplidNEW 3adant2 3eqtr3d grpridNEW 3eqtr3rd
      impbida ) DLMZFAMZGAMZNZFEOZGPZFGBQZCPZVDVFRFVEBQZVGCVFVIVGPVDVEGFBSTVDVI
      CPZVFVAVBVJVCABCDEFHIJKUAUBUCUDVDVHRVEVGBQZVECBQZGVEVHVKVLPVDVGCVEBSTVDVK
      GPVHVDVEFBQZGBQZCGBQZVKGVAVBVNVOPVCVAVBRVMCGBABCDEFHIJKUEUFUBVAVBVCVNVKPZ
      VAVBVCRZVEAMZVBVCNVPVAVQRVRVBVCVAVBVRVCABDEFHIKUGZUHVAVBVCUIVAVBVCUJUKABD
      VEFGHIULUMUNVAVCVOGPVBGABCDHIJUOUPUQUCVDVLVEPZVHVAVBVTVCVAVBVRVTVSVEABCDH
      IJURUMUBUCUSUT $.
      $( [?] $) $( [24-Aug-2011] $)

    $( The inverse of a group element expressed in terms of the identity
       element. $)
    grpinvid2NEW $p |- ( ( G e. GrpNEW /\ X e. B /\ Y e. B ) ->
                    ( ( N ` X ) = Y <-> ( Y P X ) = U ) ) $=
      ( cgrp wcel w3a cfv wceq co wa opreq1 adantl grplinvNEW 3adant3 adantr
      eqtr3d grpinvclNEW grplidNEW syldan eqcomd simprr simprl adantrr 3jca
      grpassNEW 3impb grprinvNEW opreq2d grpridNEW 3adant2 3eqtrd 3eqtr2d
      impbida ) DLMZFAMZGAMZNZFEOZGPZGFBQZCPZVEVGRVFFBQZVHCVGVJVHPVEVFGFBSTVEVJ
      CPZVGVBVCVKVDABCDEFHIJKUAUBUCUDVEVIRVFCVFBQZVHVFBQZGVEVFVLPVIVEVLVFVBVCVL
      VFPZVDVBVCVFAMZVNABDEFHIKUEZVFABCDHIJUFUGUBUHUCVIVMVLPVEVHCVFBSTVEVMGPVIV
      EVMGFVFBQZBQZGCBQZGVBVCVDVMVRPZVBVCVDRZVDVCVONVTVBWARVDVCVOVBVCVDUIVBVCVD
      UJVBVCVOVDVPUKULABDGFVFHIUMUGUNVBVCVRVSPVDVBVCRVQCGBABCDEFHIJKUOUPUBVBVDV
      SGPVCGABCDHIJUQURUSUCUTVA $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    grpinvid.1NEW $e |- U = ( 0g ` G ) $.
    grpinvid.2NEW $e |- N = ( -g ` G ) $.
    $( The inverse of the identity element of a group. $)
    grpinvidNEW $p |- ( G e. GrpNEW -> ( N ` U ) = U ) $=
      ( cgrp wcel cfv wceq cplusg co cbs eqid grpidclNEW grplidNEW mpdan wb
      grpinvid1NEW mpd3an23 mpbird ) BFGZACHAIZAABJHZKAIZUAABLHZGZUDUEUCABUEMZU
      CMZDNZAUEUCABUGUHDOPUAUFUFUBUDQUIUIUEUCABCAAUGUHDERST $.
      $( [?] $) $( [24-Aug-2011] $)
  $}

  ${
    grplcan.1NEW $e |- B = ( base ` G ) $.
    grplcan.2NEW $e |- P = ( +g ` G ) $.
    $( Left cancellation law for groups. $)
    grplcanNEW $p |- ( ( G e. GrpNEW /\ ( X e. B /\ Y e. B /\ Z e. B ) ) ->
                  ( ( Z P X ) = ( Z P Y ) <-> X = Y ) ) $=
      ( cgrp wcel w3a wa co wceq wi cminusg cfv opreq2 adantl c0g eqid
      grplinvNEW adantlr opreq1d grpinvclNEW adantrl simprr simprl 3jca
      grpassNEW syldan anassrs grplidNEW adantr 3eqtr3d adantrr exp53 3imp2
      impbid1 ) CIJZDAJZEAJZFAJZKLFDBMZFEBMZNZDENZUTVAVBVCVFVGOUTVAVBVCVFVGUTVA
      LZVBVCLZLZVFLFCPQZQZVDBMZVLVEBMZDEVFVMVNNVJVDVEVLBRSVJVMDNZVFVHVCVOVBVHVC
      LZVLFBMZDBMZCTQZDBMZVMDVPVQVSDBUTVCVQVSNZVAABVSCVKFGHVSUAZVKUAZUBZUCUDUTV
      AVCVRVMNZUTVAVCLZVLAJZVCVAKWEUTWFLWGVCVAUTVCWGVAABCVKFGHWCUEZUFUTVAVCUGUT
      VAVCUHUIABCVLFDGHUJUKULVHVTDNVCDABVSCGHWBUMUNUOUFUNVJVNENZVFUTVIWIVAUTVIL
      ZVQEBMZVSEBMZVNEWJVQVSEBUTVCWAVBWDUFUDUTVIWGVCVBKWKVNNWJWGVCVBUTVCWGVBWHU
      FUTVBVCUGUTVBVCUHUIABCVLFEGHUJUKUTVBWLENVCEABVSCGHWBUMUPUOUCUNUOUQURDEFBR
      US $.
      $( [?] $) $( [25-Aug-2011] $)
  $}

