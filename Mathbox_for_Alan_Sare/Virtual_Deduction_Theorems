$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Virtual Deduction Theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c (. $. $( Left parenthesis for virtual deduction for virtual hypotheses.
      [21-Apr-2011] $)

  $c ). $. $( Right parenthesis for virtual deduction.
      [9-Nov-2011] $)

  $c |-. $. $(  Symbol for virtual inference.
      [9-Nov-2011] $)

  ${
    $( Natural Deduction is a well known proof method orignally proposed by
       Gentzen in 1935 and comprehensively summarized by Prawitz in his 1965
       monograph "Natural deduction: a proof-theoretic study".  Gentzen wished
       to construct "a formalism that comes as close as possible to natural
       reasoning".  Natural deduction is a response to dissatisfaction with
       axiomatic proofs such as Hilbert-style axiomatic proofs, which the
       proofs of Metamath are.  In 1926, in Poland, Lukasiewicz advocated a
       more natural treatment of logic.  Jaskowski made the earliest attempts
       at defining a more natural deduction.  Natural deduction in its modern
       form was independently proposed by Gentzen.

       Natural Deduction cannot be done within a Hilbert-style deduction
       system.  This is because the objects being deduced from and the object
       being deduced are deductions.  A deduction is not a wff and therefore
       cannot cannot be a step in a Hilbert-style deduction system proof.  The
       introduction of virtual deductions make it possible to construct proofs
       in a Hilbert-style deduction systems by a means essentially equivalent
       to Natural Deduction.

       The concept of the virtual deduction is analogous to the concept of
       virtual classes, as described in Quine's "Set Theory and Its Logic",
       pages 15 to 21.  Metamath's class abstractions are virtual classes.
       Virtual classes are also used in Takeuti and Zaring's "Introduction to
       Axiomatic Set Theory".  Classes are introduced as defined terms.  As
       Quine states on page 16, " ... there remains no hint of there being such
       a thing as the class ` { x | ph } ` ".  In Takeuti and Zaring,

       ` ( x e. { y | ph } <-> [ x / y ] ph ) ` .

       ` [ x / y ] ph ` is the narrower sense wff corresponding to the wider
       sense wff ` x e. { y | ph } ` .  Every wider sense wff is an
       abbreviation for a wff in the original sense.  A set ` x ` belongs to
       the class ` { y | ph } ` if and only if ` [ x / y ] ph ` .  A class is a
       virtual object.  Although thought of as an object containing sets, class
       abstractions do not actually exist.  A class is merely a term in an
       abbreviation for a wff in which no classes occur.

       The notion of virtual classes is useful because it allows for the
       succinct resolution of Russell's paradox because virtual classes which
       are not sets cannot be members of a virtual class.

       A virtual deduction is also merely an abbreviation for a wff.


       ` ( (. ph , ps , ` ... ` , ch , ... , th |-. et ). <-> `
       ` ( ( ph /\ ps ` ... ` /\ ch ` ... ` /\ th ) -> et ) ) `

       is the definition of the virtual deduction

       ` (. ph , ps , ` ... ` , ch , ` ... ` , th |-. et ). `

       This virtual deduction is interpreted as

       ` ph , ps , ` ... ` , ch , ` ... ` , th ` infer ` et ` .

       ` ph , ps , ` ... ` , ch , ` ... ` , th ` are "hypotheses" and ` et ` is
       the "conclusion".  An alternate definition of a virtual deduction is


       ` ( (. ph , ps , ` ... ` , ch , ` ...
       ` , th |-. et ). <-> ( ph -> ( ps -> ` ... ` -> ( ch -> ` ...
       ` -> ( th -> et ) ` ... ` ) ` ... ` ) ) ) `

       Just as the abbreviation ` x e. { y | ph } ` should be thought of as
       stating that x is contained in the class ` { y | ph } ` even though the
       entity ` { y | ph } ` does not actually exist and x cannot be contained
       in a non-existent entity, the abbreviation ` (. ph , ps , ` ...
       ` , ch , ` ... ` , th |-. et ). ` should be thought of as stating that
       the hypotheses ` ph , ps , ` ... ` , ch , ` ... ` , th ` together infer
       ` et ` , even though ` ph , ps , ` ... ` , ch , ` ... ` , th ` are not
       actually hypotheses and therefore can not infer anything.

       Natural Deduction relies on the Deduction Theorem.  The Deduction
       Theorem is valid for sentences, but is not valid for all formulas.  The
       rules of Natural Deduction of a natural deduction calculus applicable to
       any wff must be such that if

       ` (. ph , ps , ` ... ` , ch , ` ... ` , th |-. et ). `

       then

       ` ( ( ph /\ ps ` ... ` /\ ch ` ... ` /\ th ) -> et ) `

       Only then is the Deduction Theorem true for that universe of wffs.  The
       satisfaction of this meta-rule is implicit in the definition of a
       virtual deduction.  Although wffs which are virtual deductions are not
       real deductions, they are meta-logically equivalent to their
       corresponding real deductions.  If

       ` (. ph , ps , ` ... ` , ch , ` ... ` , th |-. et ). `

       then

       ` ph ` & ` ps ` ... & ` ch ` ... & ` th ` infer ` et `

       Every virtual deduction meta-infers it corresponding deduction, although
       some deductions do not have a corresponding virtual deduction.  Virtual
       Natural Deduction may be employed to prove theorems or deductions in a
       Hilbert-style deduction system in a natural and intuitive manner.
       Natural Deduction is used to prove theorems or deductions within a
       Genzten system.  "Virtual Natural Deduction" or, "Virtual Deduction",
       for short, is essentially Natural Deduction for use within Hilbert-style
       deduction systems.  Hopefully, the term for the proof method, "Virtual
       Deduction", will not be confused with the term "virtual deduction", a
       type of wff.

       Proving a theorem by Virtual Deduction is proving a theorem by
       deduction, which is how all theorems within a Hilbert-style deduction
       system are proven.

       A Gentzen Natural Deduction system requires Natural Deduction axioms and
       rules of inference.  Adding the above definition of a virtual deduction
       transforms any Hilbert-style deduction system into a Virtual Natural
       Deduction system.  No additional axioms or rules of inference are
       needed.  Every step in any Virtual Deduction proof may be deduced from
       the axioms and rules of inference of the Hilbert-style deduction system
       (for those steps in which no virtual deductions occur) or those axioms
       and rules and the above virtual deduction definition (for those steps in
       which one or more virtual deductions occur).  The idn* theorems
       (identity theorems), the in* deductions (introduction rules), the e*
       deductions (elimination rules), and the gen* deductions (generalizing
       rules) are Metamath theorems or deductions deduced from the axioms,
       ~ ax-gen , and ~ ax-mp , and the virtual deduction definitions.  These
       are the only theorems or deductions in which virtual deductions occur
       which would be needed directly to construct most Virtual Deduction
       proofs.

       By introducing the virtual deduction definition, we have eliminated the
       need for a Natural Deduction system (a Gentzen system) within which to
       prove theorems by Natural Deduction or an equivalent method.  With the
       introduction of the virtual deduction definition, we may prove theorems
       within a Hilbert-style deduction system by a means essentially the same
       as Natural Deduction. $)
    vd1 $a wff (. ph |-. ps ). $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Definition of virtual deduction. $)
    df-vd1 $a |- ( (. ph |-. ps ). <-> ( ph -> ps ) ) $.
      $( [14-Nov-2011] $)
  $}

  ${
    in1.1 $e |- (. ph |-. ps ). $.
    $( Inference form of ~ df-vd1 .  Virtual deduction introduction rule of
       converting the virtual hypothesis of a 1-virtual hypothesis virtual
       deduction into an antecedent. $)
    in1 $p |- ( ph -> ps ) $=
      ( vd1 wi df-vd1 mpbi ) ABDABECABFG $.
      $( [14-Nov-2011] $)
  $}

  ${
    dfvd1ir.1 $e |- ( ph -> ps ) $.
    $( Inference form of ~ df-vd1 with the virtual deduction as the
       assertion. $)
    dfvd1ir $p |- (. ph |-. ps ). $=
      ( vd1 wi df-vd1 mpbir ) ABDABECABFG $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Virtual deduction identity rule which is ~ id with virtual deduction
       symbols. $)
    idn1 $p |- (. ph |-. ph ). $=
      ( id dfvd1ir ) AAABC $.
      $( [24-Jun-2011] $)
  $}

  ${
    $d x ph $.  $d y ph $.  $d x y $.
    $( Quantification of two variables over a formula in which they do not
       occur. $)
    ax172 $p |- ( ph -> A. x A. y ph ) $=
      ( wal ax-17 syl ) AACDZGBDACEGBEF $.
      $( [12-Apr-2011] $)
  $}

  ${
    $( Left-to-right part of definition of virtual deduction. $)
    dfvd1imp $p |- ( (. ph |-. ps ). -> ( ph -> ps ) ) $=
      ( vd1 wi df-vd1 biimpi ) ABCABDABEF $.
      $( [21-Apr-2011] $)
  $}

  ${
    $( Right-to-left part of definition of virtual deduction. $)
    dfvd1impr $p |- ( ( ph -> ps ) -> (. ph |-. ps ). ) $=
      ( vd1 wi df-vd1 biimpri ) ABCABDABEF $.
      $( [21-Apr-2011] $)
  $}

  ${
    $( Syntax for a 2-hypothesis virtual deduction. $)
    vd2 $a wff (. ph , ps |-. ch ). $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Definition of a 2-hypothesis virtual deduction. $)
    df-vd2 $a |- ( (. ph , ps |-. ch ). <-> ( ( ph /\ ps ) -> ch ) ) $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Definition of a 2-hypothesis virtual deduction. $)
    dfvd2 $p |- ( (. ph , ps |-. ch ). <-> ( ph -> ( ps -> ch ) ) ) $=
      ( vd2 wa wi df-vd2 impexp bitri ) ABCDABECFABCFFABCGABCHI $.
      $( [14-Nov-2011] $)
  $}

  ${
    dfvd2i.1 $e |- (. ph , ps |-. ch ). $.
    $( Inference form of ~ dfvd2 . $)
    dfvd2i $p |- ( ph -> ( ps -> ch ) ) $=
      ( vd2 wi dfvd2 mpbi ) ABCEABCFFDABCGH $.
      $( [14-Nov-2011] $)
  $}

  ${
    dfvd2ir.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Right-to-left inference form of ~ dfvd2 . $)
    dfvd2ir $p |- (. ph , ps |-. ch ). $=
      ( vd2 wi dfvd2 mpbir ) ABCEABCFFDABCGH $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Syntax for a 3-hypothesis virtual deduction. $)
    vd3 $a wff (. ph , ps , ch |-. th ). $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Definition of a 3-hypothesis virtual deduction. $)
    df-vd3 $a |- ( (. ph , ps , ch |-. th ). <->
                 ( ( ph /\ ps /\ ch ) -> th ) ) $.
      $( [14-Nov-2011] $)
  $}

  ${
    $( Definition of a 3-hypothesis virtual deduction. $)
    dfvd3 $p |- ( (. ph , ps , ch |-. th ). <->
                 ( ph -> ( ps -> ( ch -> th ) ) ) ) $=
      ( vd3 w3a wi df-vd3 wa df-3an imbi1i impexp bitri ) ABCDEABCFZDGZABCDGZGG
      ZABCDHOABIZPGZQORCIZDGSNTDABCJKRCDLMABPLMM $.
      $( [14-Nov-2011] $)
  $}

  ${
    dfvd3i.1 $e |- (. ph , ps , ch |-. th ). $.
    $( Inference form of ~ dfvd3 . $)
    dfvd3i $p |- ( ph -> ( ps -> ( ch -> th ) ) ) $=
      ( vd3 wi dfvd3 mpbi ) ABCDFABCDGGGEABCDHI $.
      $( [14-Nov-2011] $)
  $}

  ${
    dfvd3ir.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    $( Right-to-left inference form of ~ dfvd3 . $)
    dfvd3ir $p |- (. ph , ps , ch |-. th ). $=
      ( vd3 wi dfvd3 mpbir ) ABCDFABCDGGGEABCDHI $.
      $( [14-Nov-2011] $)
  $}

  ${
    vd01.1 $e |- ph $.
    $( A virtual hypothesis virtually infers a theorem. $)
    vd01 $p |- (. ps |-. ph ). $=
      ( a1i dfvd1ir ) BAABCDE $.
      $( [14-Jun-2011] $)
  $}

  ${
    vd02.1 $e |- ph $.
    $( 2 virtual hypotheses virtually infer a theorem. $)
    vd02 $p |- (. ps , ch |-. ph ). $=
      ( wi a1i dfvd2ir ) BCACAEBACDFFG $.
      $( [14-Jun-2011] $)
  $}

  ${
    vd03.1 $e |- ph $.
    $( A theorem is virtually inferred by the 3 virtual hypotheses. $)
    vd03 $p |- (. ps , ch , th |-. ph ). $=
      ( wi a1i dfvd3ir ) BCDACDAFZFBICADEGGGH $.
      $( [12-Jun-2011] $)
  $}

  ${
    vd12.1 $e |- (. ph |-. ps ). $.
    $( A virtual deduction with 1 virtual hypothesis virtually inferring a
       virtual conclusion infers that the same conclusion is virtually inferred
       by the same virtual hypothesis and an additional hypothesis. $)
    vd12 $p |- (. ph , ch |-. ps ). $=
      ( in1 a1d dfvd2ir ) ACBABCABDEFG $.
      $( [12-Jun-2011] $)
  $}

  ${
    vd13.1 $e |- (. ph |-. ps ). $.
    $( A virtual deduction with 1 virtual hypothesis virtually inferring a
       virtual conclusion infers that the same conclusion is virtually inferred
       by the same virtual hypothesis and a two additional hypotheses. $)
    vd13 $p |- (. ph , ch , th |-. ps ). $=
      ( in1 a1d a1dd dfvd3ir ) ACDBACBDABCABEFGHI $.
      $( [12-Jun-2011] $)
  $}

  ${
    vd23.1 $e |- (. ph , ps |-. ch ). $.
    $( A virtual deduction with 2 virtual hypotheses virtually inferring a
       virtual conclusion infers that the same conclusion is virtually inferred
       by the same 2 virtual hypotheses and a third hypothesis. $)
    vd23 $p |- (. ph , ps , th |-. ch ). $=
      ( dfvd2i a1dd dfvd3ir ) ABDCABCDABCEFGH $.
      $( [12-Jun-2011] $)
  $}

  ${
    $( The virtual deduction form of a 2-antecedent nested implication implies
       the 2-antecedent nested implication. $)
    dfvd2imp $p |- ( (. ph , ps |-. ch ). -> ( ph -> ( ps -> ch ) ) ) $=
      ( vd2 wi dfvd2 biimpi ) ABCDABCEEABCFG $.
      $( [21-Apr-2011] $)
  $}

  ${
    $( A 2-antecedent nested implication implies its virtual deduction form. $)
    dfvd2impr $p |- ( ( ph -> ( ps -> ch ) ) -> (. ph , ps |-. ch ). ) $=
      ( vd2 wi dfvd2 biimpri ) ABCDABCEEABCFG $.
      $( [21-Apr-2011] $)
  $}

  ${
    in2.1 $e |- (. ph , ps |-. ch ). $.
    $( The virtual deduction introduction rule of converting the end virtual
       hypothesis of 2 virtual hypotheses into an antecedent. $)
    in2 $p |- (. ph |-. ( ps -> ch ) ). $=
      ( wi dfvd2i dfvd1ir ) ABCEABCDFG $.
      $( [21-Apr-2011] $)
  $}

  ${
    iin2.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( ~ in2 without virtual deductions. $)
    iin2 $p |- ( ph -> ( ps -> ch ) ) $=
      (  ) D $.
      $( [20-Jun-2011] $)
  $}

  ${
    in2an.1 $e |- (. ph , ( ps /\ ch ) |-. th ). $.
    $( The virtual deduction introduction rule converting the second conjunct
       of the second virtual hypothesis into the antecedent of the conclusion.
       ~ exp3a is the non-virtual deduction form of ~ in2an . $)
    in2an $p |- (. ph , ps |-. ( ch -> th ) ). $=
      ( wi wa dfvd2i exp3a dfvd2ir ) ABCDFABCDABCGDEHIJ $.
      $( [30-Jun-2012] $)
  $}

  ${
    in3.1 $e |- (. ph , ps , ch |-. th ). $.
    $( The virtual deduction introduction rule of converting the end virtual
       hypothesis of 3 virtual hypotheses into an antecedent. $)
    in3 $p |- (. ph , ps |-. ( ch -> th ) ). $=
      ( wi dfvd3i dfvd2ir ) ABCDFABCDEGH $.
      $( [12-Jun-2011] $)
  $}

  ${
    in3an.1 $e |- (. ph , ps , ( ch /\ th ) |-. ta ). $.
    $( The virtual deduction introduction rule converting the second conjunct
       of the third virtual hypothesis into the antecedent of the conclusion.
       ~ exp4a is the non-virtual deduction form of ~ in3an . $)
    in3an $p |- (. ph , ps , ch |-. ( th -> ta ) ). $=
      ( wi wa dfvd3i exp4a dfvd3ir ) ABCDEGABCDEABCDHEFIJK $.
      $( [25-Jun-2012] $)
  $}

  ${
    $( Virtual deduction identity rule which is ~ idd with virtual deduction
       symbols. $)
    idn2 $p |- (. ph , ps |-. ps ). $=
      ( idd dfvd2ir ) ABBABCD $.
      $( [21-Apr-2011] $)
  $}

  ${
    $( Virtual deduction identity rule for 3 virtual hypotheses. $)
    idn3 $p |- (. ph , ps , ch |-. ch ). $=
      ( wi idd a1i dfvd3ir ) ABCCBCCDDABCEFG $.
      $( [11-Jun-2011] $)
  $}

  ${
    $d x ph $.
    gen11.1 $e |- (. ph |-. ps ). $.
    $( Virtual deduction generalizing rule for 1 quantifying variable and 1
       virtual hypothesis. ~ 19.21aiv is ~ gen11 without virtual deductions. $)
    gen11 $p |- (. ph |-. A. x ps ). $=
      ( wal wi vd1 dfvd1imp ax-mp 19.21aiv dfvd1impr ) ABCEZFALGABCABGABFDABHIJ
      ALKI $.
      $( [21-Apr-2011] $)
  $}

  ${
    gen11nv.1 $e |- ( ph -> A. x ph ) $.
    gen11nv.2 $e |- (. ph |-. ps ). $.
    $( Virtual deduction generalizing rule for 1 quantifying variable and 1
       virtual hypothesis without distinct variables. ~ 19.21ai is ~ gen11nv
       without virtual deductions. $)
    gen11nv $p |- (. ph |-. A. x ps ). $=
      ( wal in1 19.21ai dfvd1ir ) ABCFABCDABEGHI $.
      $( [12-Dec-2011] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    gen12.1 $e |- (. ph |-. ps ). $.
    $( Virtual deduction generalizing rule for 2 quantifying variables and 1
       virtual hypothesis. ~ gen12 is ~ 19.21aivv with virtual deductions. $)
    gen12 $p |- (. ph |-. A. x A. y ps ). $=
      ( wal in1 19.21aivv dfvd1ir ) ABDFCFABCDABEGHI $.
      $( [2-May-2011] $)
  $}

  ${
    $d x ph $.  $d x ps $.
    gen21.1 $e |- (. ph , ps |-. ch ). $.
    $( Virtual deduction generalizing rule for 1 quantifying variables and 2
       virtual hypothesis. ~ gen21 is ~ 19.21adv with virtual deductions. $)
    gen21 $p |- (. ph , ps |-. A. x ch ). $=
      ( wal dfvd2i 19.21adv dfvd2ir ) ABCDFABCDABCEGHI $.
      $( [25-Jul-2011] $)
  $}

  ${
    gen21nv.1 $e |- ( ph -> A. x ph ) $.
    gen21nv.2 $e |- ( ps -> A. x ps ) $.
    gen21nv.3 $e |- (. ph , ps |-. ch ). $.
    $( Virtual deduction form of ~ 19.21ad . $)
    gen21nv $p |- (. ph , ps |-. A. x ch ). $=
      ( wal dfvd2i 19.21ad dfvd2ir ) ABCDHABCDEFABCGIJK $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d ch x $.  $d ph x $.  $d ps x $.
    gen31.1 $e |- (. ph , ps , ch |-. th ). $.
    $( Virtual deduction generalizing rule for 1 quantifying variable and 3
       virtual hypothesis. ~ gen31 is ~ ggen31 with virtual deductions. $)
    gen31 $p |- (. ph , ps , ch |-. A. x th ). $=
      ( wal dfvd3i ggen31 dfvd3ir ) ABCDEGABCDEABCDFHIJ $.
      $( [22-Jun-2012] $)
  $}

  ${
    $d x ph $.  $d y ph $.  $d x ps $.  $d y ps $.
    gen22.1 $e |- (. ph , ps |-. ch ). $.
    $( Virtual deduction generalizing rule for 2 quantifying variables and 2
       virtual hypothesis. $)
    gen22 $p |- (. ph , ps |-. A. x A. y ch ). $=
      ( wal dfvd2i 19.21adv dfvd2ir ) ABCEGZDGABKDABCEABCFHIIJ $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d x ph $.  $d y ph $.  $d x ps $.  $d y ps $.
    ggen22.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( ~ gen22 without virtual deductions. $)
    ggen22 $p |- ( ph -> ( ps -> A. x A. y ch ) ) $=
      ( wal 19.21adv ) ABCEGDABCEFHH $.
      $( [25-Jul-2011] $)
  $}

  ${
    e1_.1 $e |- (. ph |-. ps ). $.
    e1_.2 $e |- ( ps -> ch ) $.
    $( A Virtual deduction elimination rule. ~ syl is ~ e1_ without virtual
       deductions. $)
    e1_ $p |- (. ph |-. ch ). $=
      ( in1 syl dfvd1ir ) ACABCABDFEGH $.
      $( [11-Jun-2011] $)
  $}

  ${
    e1bi.1 $e |- (. ph |-. ps ). $.
    e1bi.2 $e |- ( ps <-> ch ) $.
    $( Biconditional form of ~ e1_ . ~ sylib is ~ e1bi without virtual
       deductions. $)
    e1bi $p |- (. ph |-. ch ). $=
      ( biimpi e1_ ) ABCDBCEFG $.
      $( [15-Jun-2011] $)
  $}

  ${
    e1bir.1 $e |- (. ph |-. ps ). $.
    e1bir.2 $e |- ( ch <-> ps ) $.
    $( Right biconditional form of ~ e1_ . ~ sylibr is ~ e1bir without virtual
       deductions. $)
    e1bir $p |- (. ph |-. ch ). $=
      ( biimpri e1_ ) ABCDCBEFG $.
      $( [24-Jun-2011] $)
  $}

  ${
    e2.1 $e |- (. ph , ps |-. ch ). $.
    e2.2 $e |- ( ch -> th ) $.
    $( A virtual deduction elimination rule. ~ syl6 is ~ e2 without virtual
       deductions. $)
    e2 $p |- (. ph , ps |-. th ). $=
      ( dfvd2i syl6 dfvd2ir ) ABDABCDABCEGFHI $.
      $( [21-Apr-2011] $)
  $}

  ${
    e2bi.1 $e |- (. ph , ps |-. ch ). $.
    e2bi.2 $e |- ( ch <-> th ) $.
    $( Bi-conditional form of ~ e2 . ~ syl6ib is ~ e2bi without virtual
       deductions. $)
    e2bi $p |- (. ph , ps |-. th ). $=
      ( biimpi e2 ) ABCDECDFGH $.
      $( [10-Jun-2011] $)
  $}

  ${
    e2bir.1 $e |- (. ph , ps |-. ch ). $.
    e2bir.2 $e |- ( th <-> ch ) $.
    $( Right bi-conditional form of ~ e2 . ~ syl6ibr is ~ e2bir without virtual
       deductions. $)
    e2bir $p |- (. ph , ps |-. th ). $=
      ( biimpri e2 ) ABCDEDCFGH $.
      $( [29-Apr-2011] $)
  $}

  ${
    ee223.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee223.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee223.3 $e |- ( ph -> ( ps -> ( ta -> et ) ) ) $.
    ee223.4 $e |- ( ch -> ( th -> ( et -> ze ) ) ) $.
    $( ~ e223 without virtual deductions. $)
    ee223 $p |- ( ph -> ( ps -> ( ta -> ze ) ) ) $=
      ( wi syl6 com34 com23 com12 syl8 pm2.43a pm2.43d mpdd ) ABDEGLIABEDGABEDG
      LZLABEBUABAEBUALZLABEAUBABEFAUBLJAFUBABFUAABDFGABCDFGLLHKMNOPQNRNSNT $.
      $( [12-Dec-2011] $)
  $}

  ${
    e223.1 $e |- (. ph , ps |-. ch ). $.
    e223.2 $e |- (. ph , ps |-. th ). $.
    e223.3 $e |- (. ph , ps , ta |-. et ). $.
    e223.4 $e |- ( ch -> ( th -> ( et -> ze ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e223 $p |- (. ph , ps , ta |-. ze ). $=
      ( wi in2 in1 in3 ee223 dfvd3ir ) ABEGABCDEFGABCLABCHMNABDLABDIMNABEFLZLAB
      RABEFJOMNKPQ $.
      $( [12-Dec-2011] $)
  $}

  ${
    e222.1 $e |- (. ph , ps |-. ch ). $.
    e222.2 $e |- (. ph , ps |-. th ). $.
    e222.3 $e |- (. ph , ps |-. ta ). $.
    e222.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e222 $p |- (. ph , ps |-. et ). $=
      ( wa dfvd2i imp wi syl syl5com pm2.43i ex dfvd2ir ) ABFABFABKZFTETFABEABE
      ILMTEFNZTDTUAABDABDHLMTCDUANABCABCGLMJOPQPQRS $.
      $( [12-Jun-2011] $)
  $}

  ${
    e220.1 $e |- (. ph , ps |-. ch ). $.
    e220.2 $e |- (. ph , ps |-. th ). $.
    e220.3 $e |- ta $.
    e220.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e220 $p |- (. ph , ps |-. et ). $=
      ( vd02 e222 ) ABCDEFGHEABIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee220.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee220.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee220.3 $e |- ta $.
    ee220.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e220 without virtual deductions. $)
    ee220 $p |- ( ph -> ( ps -> et ) ) $=
      ( a1i12 ee222 ) ABCDEFGHABEIKJL $.
      $( [12-Jul-2011] $)
  $}

  ${
    e202.1 $e |- (. ph , ps |-. ch ). $.
    e202.2 $e |- th $.
    e202.3 $e |- (. ph , ps |-. ta ). $.
    e202.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e202 $p |- (. ph , ps |-. et ). $=
      ( vd02 e222 ) ABCDEFGDABHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee202.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee202.2 $e |- th $.
    ee202.3 $e |- ( ph -> ( ps -> ta ) ) $.
    ee202.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e202 without virtual deductions. $)
    ee202 $p |- ( ph -> ( ps -> et ) ) $=
      ( wi a1i ee222 ) ABCDEFGBDKADBHLLIJM $.
      $( [13-Jul-2011] $)
  $}

  ${
    e022.1 $e |- ph $.
    e022.2 $e |- (. ps , ch |-. th ). $.
    e022.3 $e |- (. ps , ch |-. ta ). $.
    e022.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e022 $p |- (. ps , ch |-. et ). $=
      ( vd02 e222 ) BCADEFABCGKHIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee022.1 $e |- ph $.
    ee022.2 $e |- ( ps -> ( ch -> th ) ) $.
    ee022.3 $e |- ( ps -> ( ch -> ta ) ) $.
    ee022.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e022 without virtual deductions. $)
    ee022 $p |- ( ps -> ( ch -> et ) ) $=
      ( wi a1i ee222 ) BCADEFCAKBACGLLHIJM $.
      $( [13-Jul-2011] $)
  $}

  ${
    e002.1 $e |- ph $.
    e002.2 $e |- ps $.
    e002.3 $e |- (. ch , th |-. ta ). $.
    e002.4 $e |- ( ph -> ( ps -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e002 $p |- (. ch , th |-. et ). $=
      ( vd02 e222 ) CDABEFACDGKBCDHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee002.1 $e |- ph $.
    ee002.2 $e |- ps $.
    ee002.3 $e |- ( ch -> ( th -> ta ) ) $.
    ee002.4 $e |- ( ph -> ( ps -> ( ta -> et ) ) ) $.
    $( ~ e002 without virtual deductions. $)
    ee002 $p |- ( ch -> ( th -> et ) ) $=
      ( wi a1i ee222 ) CDABEFDAKCADGLLDBKCBDHLLIJM $.
      $( [13-Jul-2011] $)
  $}

  ${
    e020.1 $e |- ph $.
    e020.2 $e |- (. ps , ch |-. th ). $.
    e020.3 $e |- ta $.
    e020.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e020 $p |- (. ps , ch |-. et ). $=
      ( vd02 e222 ) BCADEFABCGKHEBCIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee020.1 $e |- ph $.
    ee020.2 $e |- ( ps -> ( ch -> th ) ) $.
    ee020.3 $e |- ta $.
    ee020.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e020 without virtual deductions. $)
    ee020 $p |- ( ps -> ( ch -> et ) ) $=
      ( wi a1i ee222 ) BCADEFCAKBACGLLHCEKBECILLJM $.
      $( [13-Jul-2011] $)
  $}

  ${
    e200.1 $e |- (. ph , ps |-. ch ). $.
    e200.2 $e |- th $.
    e200.3 $e |- ta $.
    e200.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e200 $p |- (. ph , ps |-. et ). $=
      ( vd02 e222 ) ABCDEFGDABHKEABIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee200.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee200.2 $e |- th $.
    ee200.3 $e |- ta $.
    ee200.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e200 without virtual deductions. $)
    ee200 $p |- ( ph -> ( ps -> et ) ) $=
      ( wi a1i ee222 ) ABCDEFGBDKADBHLLBEKAEBILLJM $.
      $( [13-Jul-2011] $)
  $}

  ${
    e221.1 $e |- (. ph , ps |-. ch ). $.
    e221.2 $e |- (. ph , ps |-. th ). $.
    e221.3 $e |- (. ph |-. ta ). $.
    e221.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e221 $p |- (. ph , ps |-. et ). $=
      ( vd12 e222 ) ABCDEFGHAEBIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee221.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee221.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee221.3 $e |- ( ph -> ta ) $.
    ee221.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e221 without virtual deductions. $)
    ee221 $p |- ( ph -> ( ps -> et ) ) $=
      ( a1d ee222 ) ABCDEFGHAEBIKJL $.
      $( [13-Jul-2011] $)
  $}

  ${
    e212.1 $e |- (. ph , ps |-. ch ). $.
    e212.2 $e |- (. ph |-. th ). $.
    e212.3 $e |- (. ph , ps |-. ta ). $.
    e212.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e212 $p |- (. ph , ps |-. et ). $=
      ( vd12 e222 ) ABCDEFGADBHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee212.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee212.2 $e |- ( ph -> th ) $.
    ee212.3 $e |- ( ph -> ( ps -> ta ) ) $.
    ee212.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e212 without virtual deductions. $)
    ee212 $p |- ( ph -> ( ps -> et ) ) $=
      ( a1d ee222 ) ABCDEFGADBHKIJL $.
      $( [13-Jul-2011] $)
  $}

  ${
    e122.1 $e |- (. ph |-. ps ). $.
    e122.2 $e |- (. ph , ch |-. th ). $.
    e122.3 $e |- (. ph , ch |-. ta ). $.
    e122.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e122 $p |- (. ph , ch |-. et ). $=
      ( vd12 e222 ) ACBDEFABCGKHIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    e112.1 $e |- (. ph |-. ps ). $.
    e112.2 $e |- (. ph |-. ch ). $.
    e112.3 $e |- (. ph , th |-. ta ). $.
    e112.4 $e |- ( ps -> ( ch -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e112 $p |- (. ph , th |-. et ). $=
      ( vd12 e222 ) ADBCEFABDGKACDHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee112.1 $e |- ( ph -> ps ) $.
    ee112.2 $e |- ( ph -> ch ) $.
    ee112.3 $e |- ( ph -> ( th -> ta ) ) $.
    ee112.4 $e |- ( ps -> ( ch -> ( ta -> et ) ) ) $.
    $( ~ e112 without virtual deductions. $)
    ee112 $p |- ( ph -> ( th -> et ) ) $=
      ( a1d ee222 ) ADBCEFABDGKACDHKIJL $.
      $( [13-Jul-2011] $)
  $}

  ${
    e121.1 $e |- (. ph |-. ps ). $.
    e121.2 $e |- (. ph , ch |-. th ). $.
    e121.3 $e |- (. ph |-. ta ). $.
    e121.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e121 $p |- (. ph , ch |-. et ). $=
      ( vd12 e222 ) ACBDEFABCGKHAECIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    e211.1 $e |- (. ph , ps |-. ch ). $.
    e211.2 $e |- (. ph |-. th ). $.
    e211.3 $e |- (. ph |-. ta ). $.
    e211.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e211 $p |- (. ph , ps |-. et ). $=
      ( vd12 e222 ) ABCDEFGADBHKAEBIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee211.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee211.2 $e |- ( ph -> th ) $.
    ee211.3 $e |- ( ph -> ta ) $.
    ee211.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e211 without virtual deductions. $)
    ee211 $p |- ( ph -> ( ps -> et ) ) $=
      ( a1d ee222 ) ABCDEFGADBHKAEBIKJL $.
      $( [13-Jul-2011] $)
  $}

  ${
    e210.1 $e |- (. ph , ps |-. ch ). $.
    e210.2 $e |- (. ph |-. th ). $.
    e210.3 $e |- ta $.
    e210.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e210 $p |- (. ph , ps |-. et ). $=
      ( vd01 e211 ) ABCDEFGHEAIKJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee210.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee210.2 $e |- ( ph -> th ) $.
    ee210.3 $e |- ta $.
    ee210.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e210 without virtual deductions. $)
    ee210 $p |- ( ph -> ( ps -> et ) ) $=
      ( a1d wi a1i ee222 ) ABCDEFGADBHKBELAEBIMMJN $.
      $( [14-Jul-2011] $)
  $}

  ${
    e201.1 $e |- (. ph , ps |-. ch ). $.
    e201.2 $e |- th $.
    e201.3 $e |- (. ph |-. ta ). $.
    e201.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e201 $p |- (. ph , ps |-. et ). $=
      ( vd01 e211 ) ABCDEFGDAHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee201.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee201.2 $e |- th $.
    ee201.3 $e |- ( ph -> ta ) $.
    ee201.4 $e |- ( ch -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e201 without virtual deductions. $)
    ee201 $p |- ( ph -> ( ps -> et ) ) $=
      ( wi a1i a1d ee222 ) ABCDEFGBDKADBHLLAEBIMJN $.
      $( [14-Jul-2011] $)
  $}

  ${
    e120.1 $e |- (. ph |-. ps ). $.
    e120.2 $e |- (. ph , ch |-. th ). $.
    e120.3 $e |- ta $.
    e120.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e120 $p |- (. ph , ch |-. et ). $=
      ( vd12 e220 ) ACBDEFABCGKHIJL $.
      $( [10-Jun-2011] $)
  $}

  ${
    ee120.1 $e |- ( ph -> ps ) $.
    ee120.2 $e |- ( ph -> ( ch -> th ) ) $.
    ee120.3 $e |- ta $.
    ee120.4 $e |- ( ps -> ( th -> ( ta -> et ) ) ) $.
    $( Virtual deduction rule ~ e120 without virtual deduction symbols. $)
    ee120 $p |- ( ph -> ( ch -> et ) ) $=
      ( a1d wi a1i ee222 ) ACBDEFABCGKHCELAECIMMJN $.
      $( [14-Jul-2011] $)
  $}

  ${
    e021.1 $e |- ph $.
    e021.2 $e |- (. ps , ch |-. th ). $.
    e021.3 $e |- (. ps |-. ta ). $.
    e021.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e021 $p |- (. ps , ch |-. et ). $=
      ( vd01 e121 ) BACDEFABGKHIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee021.1 $e |- ph $.
    ee021.2 $e |- ( ps -> ( ch -> th ) ) $.
    ee021.3 $e |- ( ps -> ta ) $.
    ee021.4 $e |- ( ph -> ( th -> ( ta -> et ) ) ) $.
    $( ~ e021 without virtual deductions. $)
    ee021 $p |- ( ps -> ( ch -> et ) ) $=
      ( wi a1i a1d ee222 ) BCADEFCAKBACGLLHBECIMJN $.
      $( [14-Jul-2011] $)
  $}

  ${
    e012.1 $e |- ph $.
    e012.2 $e |- (. ps |-. ch ). $.
    e012.3 $e |- (. ps , th |-. ta ). $.
    e012.4 $e |- ( ph -> ( ch -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e012 $p |- (. ps , th |-. et ). $=
      ( vd01 e112 ) BACDEFABGKHIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee012.1 $e |- ph $.
    ee012.2 $e |- ( ps -> ch ) $.
    ee012.3 $e |- ( ps -> ( th -> ta ) ) $.
    ee012.4 $e |- ( ph -> ( ch -> ( ta -> et ) ) ) $.
    $( ~ e012 without virtual deductions. $)
    ee012 $p |- ( ps -> ( th -> et ) ) $=
      ( wi a1i a1d ee222 ) BDACEFDAKBADGLLBCDHMIJN $.
      $( [14-Jul-2011] $)
  $}


  ${
    e102.1 $e |- (. ph |-. ps ). $.
    e102.2 $e |- ch $.
    e102.3 $e |- (. ph , th |-. ta ). $.
    e102.4 $e |- ( ps -> ( ch -> ( ta -> et ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e102 $p |- (. ph , th |-. et ). $=
      ( vd01 e112 ) ABCDEFGCAHKIJL $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee102.1 $e |- ( ph -> ps ) $.
    ee102.2 $e |- ch $.
    ee102.3 $e |- ( ph -> ( th -> ta ) ) $.
    ee102.4 $e |- ( ps -> ( ch -> ( ta -> et ) ) ) $.
    $( ~ e102 without virtual deductions. $)
    ee102 $p |- ( ph -> ( th -> et ) ) $=
      ( a1d wi a1i ee222 ) ADBCEFABDGKDCLACDHMMIJN $.
      $( [14-Jul-2011] $)
  $}

  ${
    e22.1 $e |- (. ph , ps |-. ch ). $.
    e22.2 $e |- (. ph , ps |-. th ). $.
    e22.3 $e |- ( ch -> ( th -> ta ) ) $.
    $( A virtual deduction elimination rule. $)
    e22 $p |- (. ph , ps |-. ta ). $=
      ( wi a1i e222 ) ABCCDEFFGCDEIICHJK $.
      $( [2-May-2011] $)
  $}

  ${
    e22an.1 $e |- (. ph , ps |-. ch ). $.
    e22an.2 $e |- (. ph , ps |-. th ). $.
    e22an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( Conjunction form of ~ e22 . $)
    e22an $p |- (. ph , ps |-. ta ). $=
      ( ex e22 ) ABCDEFGCDEHIJ $.
      $( [11-Jun-2011] $)
  $}

  ${
    ee22an.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee22an.2 $e |- ( ph -> ( ps -> th ) ) $.
    ee22an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( ~ e22an without virtual deductions. $)
    ee22an $p |- ( ph -> ( ps -> ta ) ) $=
      ( ex ee22 ) ABCDEFGCDEHIJ $.
      $( [8-Jul-2011] $)
  $}

  ${
    e111.1 $e |- (. ph |-. ps ). $.
    e111.2 $e |- (. ph |-. ch ). $.
    e111.3 $e |- (. ph |-. th ). $.
    e111.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e111 $p |- (. ph |-. ta ). $=
      ( in1 wi syl syl5com pm2.43i dfvd1ir ) AEAEADAEADHJADEKZACAPACGJABCPKABFJ
      ILMNMNO $.
      $( [14-Jun-2011] $)
  $}

  ${
    e1111.1 $e |- (. ph |-. ps ). $.
    e1111.2 $e |- (. ph |-. ch ). $.
    e1111.3 $e |- (. ph |-. th ). $.
    e1111.4 $e |- (. ph |-. ta ). $.
    e1111.5 $e |- ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e1111 $p |- (. ph |-. et ). $=
      ( in1 ee1111 dfvd1ir ) AFABCDEFABGLACHLADILAEJLKMN $.
      $( [6-Mar-2012] $)
  $}

  ${
    e110.1 $e |- (. ph |-. ps ). $.
    e110.2 $e |- (. ph |-. ch ). $.
    e110.3 $e |- th $.
    e110.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e110 $p |- (. ph |-. ta ). $=
      ( vd01 e111 ) ABCDEFGDAHJIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee110.1 $e |- ( ph -> ps ) $.
    ee110.2 $e |- ( ph -> ch ) $.
    ee110.3 $e |- th $.
    ee110.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( ~ e110 without virtual deductions. $)
    ee110 $p |- ( ph -> ta ) $=
      ( a1i syl3c ) ABCDEFGDAHJIK $.
      $( [22-Jul-2011] $)
  $}

  ${
    e101.1 $e |- (. ph |-. ps ). $.
    e101.2 $e |- ch $.
    e101.3 $e |- (. ph |-. th ). $.
    e101.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e101 $p |- (. ph |-. ta ). $=
      ( vd01 e111 ) ABCDEFCAGJHIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee101.1 $e |- ( ph -> ps ) $.
    ee101.2 $e |- ch $.
    ee101.3 $e |- ( ph -> th ) $.
    ee101.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( ~ e101 without virtual deductions. $)
    ee101 $p |- ( ph -> ta ) $=
      ( a1i syl3c ) ABCDEFCAGJHIK $.
      $( [23-Jul-2011] $)
  $}

  ${
    e011.1 $e |- ph $.
    e011.2 $e |- (. ps |-. ch ). $.
    e011.3 $e |- (. ps |-. th ). $.
    e011.4 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e011 $p |- (. ps |-. ta ). $=
      ( vd01 e111 ) BACDEABFJGHIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee011.1 $e |- ph $.
    ee011.2 $e |- ( ps -> ch ) $.
    ee011.3 $e |- ( ps -> th ) $.
    ee011.4 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    $( ~ e011 without virtual deductions. $)
    ee011 $p |- ( ps -> ta ) $=
      ( a1i syl3c ) BACDEABFJGHIK $.
      $( [25-Jul-2011] $)
  $}

  ${
    e100.1 $e |- (. ph |-. ps ). $.
    e100.2 $e |- ch $.
    e100.3 $e |- th $.
    e100.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e100 $p |- (. ph |-. ta ). $=
      ( vd01 e111 ) ABCDEFCAGJDAHJIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee100.1 $e |- ( ph -> ps ) $.
    ee100.2 $e |- ch $.
    ee100.3 $e |- th $.
    ee100.4 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    $( ~ e100 without virtual deductions. $)
    ee100 $p |- ( ph -> ta ) $=
      ( a1i syl3c ) ABCDEFCAGJDAHJIK $.
      $( [23-Jul-2011] $)
  $}

  ${
    e010.1 $e |- ph $.
    e010.2 $e |- (. ps |-. ch ). $.
    e010.3 $e |- th $.
    e010.4 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e010 $p |- (. ps |-. ta ). $=
      ( vd01 e111 ) BACDEABFJGDBHJIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee010.1 $e |- ph $.
    ee010.2 $e |- ( ps -> ch ) $.
    ee010.3 $e |- th $.
    ee010.4 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    $( ~ e010 without virtual deductions. $)
    ee010 $p |- ( ps -> ta ) $=
      ( a1i syl3c ) BACDEABFJGDBHJIK $.
      $( [23-Jul-2011] $)
  $}

  ${
    e001.1 $e |- ph $.
    e001.2 $e |- ps $.
    e001.3 $e |- (. ch |-. th ). $.
    e001.4 $e |- ( ph -> ( ps -> ( th -> ta ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e001 $p |- (. ch |-. ta ). $=
      ( vd01 e111 ) CABDEACFJBCGJHIK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee001.1 $e |- ph $.
    ee001.2 $e |- ps $.
    ee001.3 $e |- ( ch -> th ) $.
    ee001.4 $e |- ( ph -> ( ps -> ( th -> ta ) ) ) $.
    $( ~ e001 without virtual deductions. $)
    ee001 $p |- ( ch -> ta ) $=
      ( a1i syl3c ) CABDEACFJBCGJHIK $.
      $( [23-Jul-2011] $)
  $}

  ${
    e11.1 $e |- (. ph |-. ps ). $.
    e11.2 $e |- (. ph |-. ch ). $.
    e11.3 $e |- ( ps -> ( ch -> th ) ) $.
    $( A virtual deduction elimination rule. $)
    e11 $p |- (. ph |-. th ). $=
      ( wi a1i e111 ) ABBCDEEFBCDHHBGIJ $.
      $( [14-Jun-2011] $)
  $}

  ${
    e11an.1 $e |- (. ph |-. ps ). $.
    e11an.2 $e |- (. ph |-. ch ). $.
    e11an.3 $e |- ( ( ps /\ ch ) -> th ) $.
    $( Conjunction form of ~ e11 . $)
    e11an $p |- (. ph |-. th ). $=
      ( ex e11 ) ABCDEFBCDGHI $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee11an.1 $e |- ( ph -> ps ) $.
    ee11an.2 $e |- ( ph -> ch ) $.
    ee11an.3 $e |- ( ( ps /\ ch ) -> th ) $.
    $( ~ e11an without virtual deductions. ~ syl22anc is also ~ e11an without
       virtual deductions, exept with a different order of hypotheses. $)
    ee11an $p |- ( ph -> th ) $=
      ( ex sylc ) ABCDEFBCDGHI $.
      $( [8-Jul-2011] $)
  $}

  ${
    e01.1 $e |- ph $.
    e01.2 $e |- (. ps |-. ch ). $.
    e01.3 $e |- ( ph -> ( ch -> th ) ) $.
    $( A virtual deduction elimination rule. $)
    e01 $p |- (. ps |-. th ). $=
      ( vd01 e11 ) BACDABEHFGI $.
      $( [25-Jul-2011] $)
  $}

  ${
    ee01.1 $e |- ph $.
    ee01.2 $e |- ( ps -> ch ) $.
    ee01.3 $e |- ( ph -> ( ch -> th ) ) $.
    $( ~ e01 without virtual deduction symbols. $)
    ee01 $p |- ( ps -> th ) $=
      ( a1i sylc ) BACDABEHFGI $.
      $( [20-Apr-2011] $)
  $}

  ${
    e01an.1 $e |- ph $.
    e01an.2 $e |- (. ps |-. ch ). $.
    e01an.3 $e |- ( ( ph /\ ch ) -> th ) $.
    $( Conjunction form of ~ e01 . $)
    e01an $p |- (. ps |-. th ). $=
      ( ex e01 ) ABCDEFACDGHI $.
      $( [11-Jun-2011] $)
  $}

  ${
    ee01an.1 $e |- ph $.
    ee01an.2 $e |- ( ps -> ch ) $.
    ee01an.3 $e |- ( ( ph /\ ch ) -> th ) $.
    $( ~ e01an without virtual deductions. ~ sylancr is also a form of ~ e01an
       without virtual deduction, except the order of the hypotheses is
       different. $)
    ee01an $p |- ( ps -> th ) $=
      ( sylancr ) BACDEFGH $.
      $( [25-Jul-2011] $)
  $}

  ${
    e10.1 $e |- (. ph |-. ps ). $.
    e10.2 $e |- ch $.
    e10.3 $e |- ( ps -> ( ch -> th ) ) $.
    $( A virtual deduction elimination rule. $)
    e10 $p |- (. ph |-. th ). $=
      ( vd01 e11 ) ABCDECAFHGI $.
      $( [14-Jun-2011] $)
  $}

  ${
    e10an.1 $e |- (. ph |-. ps ). $.
    e10an.2 $e |- ch $.
    e10an.3 $e |- ( ( ps /\ ch ) -> th ) $.
    $( Conjunction form of ~ e10 . $)
    e10an $p |- (. ph |-. th ). $=
      ( ex e10 ) ABCDEFBCDGHI $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee10an.1 $e |- ( ph -> ps ) $.
    ee10an.2 $e |- ch $.
    ee10an.3 $e |- ( ( ps /\ ch ) -> th ) $.
    $( ~ e10an without virtual deductions. ~ sylancl is also ~ e10an without
       virtual deductions, except the order of the hypotheses is different. $)
    ee10an $p |- ( ph -> th ) $=
      ( sylancl ) ABCDEFGH $.
      $( [25-Jul-2011] $)
  $}

  ${
    e02.1 $e |- ph $.
    e02.2 $e |- (. ps , ch |-. th ). $.
    e02.3 $e |- ( ph -> ( th -> ta ) ) $.
    $( A virtual deduction elimination rule. $)
    e02 $p |- (. ps , ch |-. ta ). $=
      ( vd02 e22 ) BCADEABCFIGHJ $.
      $( [14-Jun-2011] $)
  $}

  ${
    e02an.1 $e |- ph $.
    e02an.2 $e |- (. ps , ch |-. th ). $.
    e02an.3 $e |- ( ( ph /\ th ) -> ta ) $.
    $( Conjunction form of ~ e02 . $)
    e02an $p |- (. ps , ch |-. ta ). $=
      ( ex e02 ) ABCDEFGADEHIJ $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee02an.1 $e |- ph $.
    ee02an.2 $e |- ( ps -> ( ch -> th ) ) $.
    ee02an.3 $e |- ( ( ph /\ th ) -> ta ) $.
    $( ~ e02an without virtual deductions. $)
    ee02an $p |- ( ps -> ( ch -> ta ) ) $=
      ( ex ee02 ) ABCDEFGADEHIJ $.
      $( [8-Jul-2011] $)
  $}

  ${
    e12.1 $e |- (. ph |-. ps ). $.
    e12.2 $e |- (. ph , ch |-. th ). $.
    e12.3 $e |- ( ps -> ( th -> ta ) ) $.
    $( A virtual deduction elimination rule. $)
    e12 $p |- (. ph , ch |-. ta ). $=
      ( vd12 e22 ) ACBDEABCFIGHJ $.
      $( [21-Apr-2011] $)
  $}

  ${
    e12an.1 $e |- (. ph |-. ps ). $.
    e12an.2 $e |- (. ph , ch |-. th ). $.
    e12an.3 $e |- ( ( ps /\ th ) -> ta ) $.
    $( Conjunction form of ~ e12 . $)
    e12an $p |- (. ph , ch |-. ta ). $=
      ( ex e12 ) ABCDEFGBDEHIJ $.
      $( [11-Jun-2011] $)
  $}

  ${

    e20.1 $e |- (. ph , ps |-. ch ). $.
    e20.2 $e |- th $.
    e20.3 $e |- ( ch -> ( th -> ta ) ) $.
    $( A virtual deduction elimination rule. $)
    e20 $p |- (. ph , ps |-. ta ). $=
      ( vd02 e22 ) ABCDEFDABGIHJ $.
      $( [14-Jun-2011] $)
  $}

  ${

    e20an.1 $e |- (. ph , ps |-. ch ). $.
    e20an.2 $e |- th $.
    e20an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( Conjunction form of ~ e20 . $)
    e20an $p |- (. ph , ps |-. ta ). $=
      ( ex e20 ) ABCDEFGCDEHIJ $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee20an.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee20an.2 $e |- th $.
    ee20an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( ~ e20an without virtual deductions. $)
    ee20an $p |- ( ph -> ( ps -> ta ) ) $=
      ( ex syl6mpi ) ABCDEFGCDEHIJ $.
      $( [8-Jul-2011] $)
  $}

  ${

    e21.1 $e |- (. ph , ps |-. ch ). $.
    e21.2 $e |- (. ph |-. th ). $.
    e21.3 $e |- ( ch -> ( th -> ta ) ) $.
    $( A virtual deduction elimination rule. $)
    e21 $p |- (. ph , ps |-. ta ). $=
      ( vd12 e22 ) ABCDEFADBGIHJ $.
      $( [12-Jun-2011] $)
  $}

  ${

    e21an.1 $e |- (. ph , ps |-. ch ). $.
    e21an.2 $e |- (. ph |-. th ). $.
    e21an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( Conjunction form of ~ e21 . $)
    e21an $p |- (. ph , ps |-. ta ). $=
      ( ex e21 ) ABCDEFGCDEHIJ $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee21an.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee21an.2 $e |- ( ph -> th ) $.
    ee21an.3 $e |- ( ( ch /\ th ) -> ta ) $.
    $( ~ e21an without virtual deductions. $)
    ee21an $p |- ( ph -> ( ps -> ta ) ) $=
      ( ex ee21 ) ABCDEFGCDEHIJ $.
      $( [8-Jul-2011] $)
  $}

  ${
    e333.1 $e |- (. ph , ps , ch |-. th ). $.
    e333.2 $e |- (. ph , ps , ch |-. ta ). $.
    e333.3 $e |- (. ph , ps , ch |-. et ). $.
    e333.4 $e |- ( th -> ( ta -> ( et -> ze ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e333 $p |- (. ph , ps , ch |-. ze ). $=
      ( w3a dfvd3i 3imp wi syl syl5com pm2.43i 3exp dfvd3ir ) ABCGABCGABCLZGUAF
      UAGABCFABCFJMNUAFGOZUAEUAUBABCEABCEIMNUADEUBOABCDABCDHMNKPQRQRST $.
      $( [12-Jun-2011] $)
  $}

  ${
    e33.1 $e |- (. ph , ps , ch |-. th ). $.
    e33.2 $e |- (. ph , ps , ch |-. ta ). $.
    e33.3 $e |- ( th -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e33 $p |- (. ph , ps , ch |-. et ). $=
      ( wi a1i e333 ) ABCDDEFGGHDEFJJDIKL $.
      $( [12-Jun-2011] $)
  $}

  ${
    e33an.1 $e |- (. ph , ps , ch |-. th ). $.
    e33an.2 $e |- (. ph , ps , ch |-. ta ). $.
    e33an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( Conjunction form of ~ e33 . $)
    e33an $p |- (. ph , ps , ch |-. et ). $=
      ( ex e33 ) ABCDEFGHDEFIJK $.
      $( [15-Jun-2011] $)
  $}

  ${
    ee33an.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee33an.2 $e |- ( ph -> ( ps -> ( ch -> ta ) ) ) $.
    ee33an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( ~ e33an without virtual deductions. $)
    ee33an $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( ex ee33 ) ABCDEFGHDEFIJK $.
      $( [8-Jul-2011] $)
  $}

  ${
    e3.1 $e |- (. ph , ps , ch |-. th ). $.
    e3.2 $e |- ( th -> ta ) $.
    $( Meta-connective form of ~ syl8 . $)
    e3 $p |- (. ph , ps , ch |-. ta ). $=
      ( wi a1i e33 ) ABCDDEFFDEHDGIJ $.
      $( [15-Jun-2011] $)
  $}

  ${
    e3bi.1 $e |- (. ph , ps , ch |-. th ). $.
    e3bi.2 $e |- ( th <-> ta ) $.
    $( Biconditional form of ~ e3 . ~ syl8ib is ~ e3bi without virtual
       deductions. $)
    e3bi $p |- (. ph , ps , ch |-. ta ). $=
      ( biimpi e3 ) ABCDEFDEGHI $.
      $( [15-Jun-2011] $)
  $}

  ${
    e3bir.1 $e |- (. ph , ps , ch |-. th ). $.
    e3bir.2 $e |- ( ta <-> th ) $.
    $( Right biconditional form of ~ e3 . $)
    e3bir $p |- (. ph , ps , ch |-. ta ). $=
      ( biimpri e3 ) ABCDEFEDGHI $.
      $( [15-Jun-2011] $)
  $}

  ${
    e03.1 $e |- ph $.
    e03.2 $e |- (. ps , ch , th |-. ta ). $.
    e03.3 $e |- ( ph -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e03 $p |- (. ps , ch , th |-. et ). $=
      ( vd03 e33 ) BCDAEFABCDGJHIK $.
      $( [12-Jun-2011] $)
  $}

  ${
    ee03.1 $e |- ph $.
    ee03.2 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    ee03.3 $e |- ( ph -> ( ta -> et ) ) $.
    $( ~ e03 without virtual deductions. $)
    ee03 $p |- ( ps -> ( ch -> ( th -> et ) ) ) $=
      ( a1i a1d a1dd ee33 ) BCDAEFBCADBACABGJKLHIM $.
      $( [17-Jul-2011] $)
  $}

  ${
    e03an.1 $e |- ph $.
    e03an.2 $e |- (. ps , ch , th |-. ta ). $.
    e03an.3 $e |- ( ( ph /\ ta ) -> et ) $.
    $( Conjunction form of ~ e03 . $)
    e03an $p |- (. ps , ch , th |-. et ). $=
      ( ex e03 ) ABCDEFGHAEFIJK $.
      $( [12-Jun-2011] $)
  $}

  ${
    ee03an.1 $e |- ph $.
    ee03an.2 $e |- ( ps -> ( ch -> ( th -> ta ) ) ) $.
    ee03an.3 $e |- ( ( ph /\ ta ) -> et ) $.
    $( Conjunction form of ~ ee03 . $)
    ee03an $p |- ( ps -> ( ch -> ( th -> et ) ) ) $=
      ( ex ee03 ) ABCDEFGHAEFIJK $.
      $( [18-Jul-2011] $)
  $}

  ${
    e30.1 $e |- (. ph , ps , ch |-. th ). $.
    e30.2 $e |- ta $.
    e30.3 $e |- ( th -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e30 $p |- (. ph , ps , ch |-. et ). $=
      ( vd03 e33 ) ABCDEFGEABCHJIK $.
      $( [12-Jun-2011] $)
  $}

  ${
    ee30.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee30.2 $e |- ta $.
    ee30.3 $e |- ( th -> ( ta -> et ) ) $.
    $( ~ e30 without virtual deductions. $)
    ee30 $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( wi a1i ee33 ) ABCDEFGBCEJZJAMBECHKKKIL $.
      $( [17-Jul-2011] $)
  $}

  ${
    e30an.1 $e |- (. ph , ps , ch |-. th ). $.
    e30an.2 $e |- ta $.
    e30an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( A virtual deduction elimination rule. $)
    e30an $p |- (. ph , ps , ch |-. et ). $=
      ( ex e30 ) ABCDEFGHDEFIJK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee30an.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee30an.2 $e |- ta $.
    ee30an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( Conjunction form of ~ ee30 . $)
    ee30an $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( ex ee30 ) ABCDEFGHDEFIJK $.
      $( [17-Jul-2011] $)
  $}

  ${
    e13.1 $e |- (. ph |-. ps ). $.
    e13.2 $e |- (. ph , ch , th |-. ta ). $.
    e13.3 $e |- ( ps -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e13 $p |- (. ph , ch , th |-. et ). $=
      ( vd13 e33 ) ACDBEFABCDGJHIK $.
      $( [13-Jun-2011] $)
  $}

  ${
    e13an.1 $e |- (. ph |-. ps ). $.
    e13an.2 $e |- (. ph , ch , th |-. ta ). $.
    e13an.3 $e |- ( ( ps /\ ta ) -> et ) $.
    $( A virtual deduction elimination rule. $)
    e13an $p |- (. ph , ch , th |-. et ). $=
      ( ex e13 ) ABCDEFGHBEFIJK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee13an.1 $e |- ( ph -> ps ) $.
    ee13an.2 $e |- ( ph -> ( ch -> ( th -> ta ) ) ) $.
    ee13an.3 $e |- ( ( ps /\ ta ) -> et ) $.
    $( ~ e13an without virtual deductions. $)
    ee13an $p |- ( ph -> ( ch -> ( th -> et ) ) ) $=
      ( ex ee13 ) ABCDEFGHBEFIJK $.
      $( [8-Jul-2011] $)
  $}

  ${
    e31.1 $e |- (. ph , ps , ch |-. th ). $.
    e31.2 $e |- (. ph |-. ta ). $.
    e31.3 $e |- ( th -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e31 $p |- (. ph , ps , ch |-. et ). $=
      ( vd13 e33 ) ABCDEFGAEBCHJIK $.
      $( [13-Jun-2011] $)
  $}

  ${
    ee31.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee31.2 $e |- ( ph -> ta ) $.
    ee31.3 $e |- ( th -> ( ta -> et ) ) $.
    $( ~ e31 without virtual deductions. $)
    ee31 $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( wi a1d ee33 ) ABCDEFGACEJBAECHKKIL $.
      $( [25-Jul-2011] $)
  $}

  ${
    e31an.1 $e |- (. ph , ps , ch |-. th ). $.
    e31an.2 $e |- (. ph |-. ta ). $.
    e31an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( A virtual deduction elimination rule. $)
    e31an $p |- (. ph , ps , ch |-. et ). $=
      ( ex e31 ) ABCDEFGHDEFIJK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee31an.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee31an.2 $e |- ( ph -> ta ) $.
    ee31an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( ~ e31an without virtual deductions. $)
    ee31an $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( wi a1d ee33an ) ABCDEFGACEJBAECHKKIL $.
      $( [14-Jul-2011] $)
  $}

  ${
    e23.1 $e |- (. ph , ps |-. ch ). $.
    e23.2 $e |- (. ph , ps , th |-. ta ). $.
    e23.3 $e |- ( ch -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e23 $p |- (. ph , ps , th |-. et ). $=
      ( vd23 e33 ) ABDCEFABCDGJHIK $.
      $( [12-Jun-2011] $)
  $}

  ${
    e23an.1 $e |- (. ph , ps |-. ch ). $.
    e23an.2 $e |- (. ph , ps , th |-. ta ). $.
    e23an.3 $e |- ( ( ch /\ ta ) -> et ) $.
    $( A virtual deduction elimination rule. $)
    e23an $p |- (. ph , ps , th |-. et ). $=
      ( ex e23 ) ABCDEFGHCEFIJK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee23an.1 $e |- ( ph -> ( ps -> ch ) ) $.
    ee23an.2 $e |- ( ph -> ( ps -> ( th -> ta ) ) ) $.
    ee23an.3 $e |- ( ( ch /\ ta ) -> et ) $.
    $( ~ e23an without virtual deductions. $)
    ee23an $p |- ( ph -> ( ps -> ( th -> et ) ) ) $=
      ( a1dd ee33an ) ABDCEFABCDGJHIK $.
      $( [14-Jul-2011] $)
  $}

  ${
    e32.1 $e |- (. ph , ps , ch |-. th ). $.
    e32.2 $e |- (. ph , ps |-. ta ). $.
    e32.3 $e |- ( th -> ( ta -> et ) ) $.
    $( A virtual deduction elimination rule. $)
    e32 $p |- (. ph , ps , ch |-. et ). $=
      ( vd23 e33 ) ABCDEFGABECHJIK $.
      $( [12-Jun-2011] $)
  $}

  ${
    ee32.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee32.2 $e |- ( ph -> ( ps -> ta ) ) $.
    ee32.3 $e |- ( th -> ( ta -> et ) ) $.
    $( ~ e32 without virtual deductions. $)
    ee32 $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( a1dd ee33 ) ABCDEFGABECHJIK $.
      $( [18-Jul-2011] $)
  $}

  ${
    e32an.1 $e |- (. ph , ps , ch |-. th ). $.
    e32an.2 $e |- (. ph , ps |-. ta ). $.
    e32an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( A virtual deduction elimination rule. $)
    e32an $p |- (. ph , ps , ch |-. et ). $=
      ( ex e32 ) ABCDEFGHDEFIJK $.
      $( [24-Jun-2011] $)
  $}

  ${
    ee32an.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    ee32an.2 $e |- ( ph -> ( ps -> ta ) ) $.
    ee32an.3 $e |- ( ( th /\ ta ) -> et ) $.
    $( ~ e33an without virtual deductions. $)
    ee32an $p |- ( ph -> ( ps -> ( ch -> et ) ) ) $=
      ( a1dd ee33an ) ABCDEFGABECHJIK $.
      $( [14-Jul-2011] $)
  $}

  ${
    e123.1 $e |- (. ph |-. ps ). $.
    e123.2 $e |- (. ph , ch |-. th ). $.
    e123.3 $e |- (. ph , ch , ta |-. et ). $.
    e123.4 $e |- ( ps -> ( th -> ( et -> ze ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e123 $p |- (. ph , ch , ta |-. ze ). $=
      ( vd13 vd23 e333 ) ACEBDFGABCEHLACDEIMJKN $.
      $( [12-Jun-2011] $)
  $}

  ${
    ee123.1 $e |- ( ph -> ps ) $.
    ee123.2 $e |- ( ph -> ( ch -> th ) ) $.
    ee123.3 $e |- ( ph -> ( ch -> ( ta -> et ) ) ) $.
    ee123.4 $e |- ( ps -> ( th -> ( et -> ze ) ) ) $.
    $( ~ e123 without virtual deductions. $)
    ee123 $p |- ( ph -> ( ch -> ( ta -> ze ) ) ) $=
      ( wi a1d a1dd ee333 ) ACEBDFGAEBLCABEHMMACDEINJKO $.
      $( [25-Jul-2011] $)
  $}

  ${
    e233.1 $e |- (. ph , ps |-. ch ). $.
    e233.2 $e |- (. ph , ps , th |-. ta ). $.
    e233.3 $e |- (. ph , ps , th |-. et ). $.
    e233.4 $e |- ( ch -> ( ta -> ( et -> ze ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e233 $p |- (. ph , ps , th |-. ze ). $=
      ( dfvd2i dfvd3i iin3 ee233 dfvd3ir ) ABDGABCDEFGABCHLABDEIMABDFJMCEFGKNOP
      $.
      $( [29-Feb-2012] $)
  $}

  ${
    e323.1 $e |- (. ph , ps , ch |-. th ). $.
    e323.2 $e |- (. ph , ps |-. ta ). $.
    e323.3 $e |- (. ph , ps , ch |-. et ). $.
    e323.4 $e |- ( th -> ( ta -> ( et -> ze ) ) ) $.
    $( A virtual deduction elimination rule. $)
    e323 $p |- (. ph , ps , ch |-. ze ). $=
      ( dfvd3i dfvd2i ee323 dfvd3ir ) ABCGABCDEFGABCDHLABEIMABCFJLKNO $.
      $( [17-Apr-2012] $)
  $}

  ${
    e000.1 $e |- ph $.
    e000.2 $e |- ps $.
    e000.3 $e |- ch $.
    e000.4 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    $( A virtual deduction elimination rule.  The non-virtual deduction form of
       ~ e000 is the virtual deduction form. $)
    e000 $p |- th $=
      ( wi mp2 ax-mp ) CDGABCDIEFHJK $.
      $( [14-Jun-2011] $)
  $}

  ${
    e00.1 $e |- ph $.
    e00.2 $e |- ps $.
    e00.3 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Elimination rule identical to ~ mp2 .  The non-virtual deduction form is
       the virtual deduction form, which is ~ mp2 . $)
    e00 $p |- ch $=
      ( mp2 ) ABCDEFG $.
      $( [14-Jun-2011] $)
  $}

  ${
    e00an.1 $e |- ph $.
    e00an.2 $e |- ps $.
    e00an.3 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( Elimination rule identical to ~ mp2an .  The non-virtual deduction form
       is the virtual deduction form, which is ~ mp2an . $)
    e00an $p |- ch $=
      ( mp2an ) ABCDEFG $.
      $( [15-Jun-2011] $)
  $}

  ${
    e0_.1 $e |- ph $.
    e0_.2 $e |- ( ph -> ps ) $.
    $( Elimination rule identical to ~ ax-mp .  The non-virtual deduction form
       is the virtual deduction form, which is ~ ax-mp . $)
    e0_ $p |- ps $=
      ( ax-mp ) ABCDE $.
      $( [14-Jun-2011] $)
  $}

  ${
    e0bi.1 $e |- ph $.
    e0bi.2 $e |- ( ph <-> ps ) $.
    $( Elimination rule identical to ~ mpbi .  The non-virtual deduction form
       is the virtual deduction form, which is ~ mpbi . $)
    e0bi $p |- ps $=
      ( mpbi ) ABCDE $.
      $( [15-Jun-2011] $)
  $}

  ${
    e0bir.1 $e |- ph $.
    e0bir.2 $e |- ( ps <-> ph ) $.
    $( Elimination rule identical to ~ mpbir .  The non-virtual deduction form
       is the virtual deduction form, which is ~ mpbir . $)
    e0bir $p |- ps $=
      ( mpbir ) BACDE $.
      $( [15-Jun-2011] $)
  $}

