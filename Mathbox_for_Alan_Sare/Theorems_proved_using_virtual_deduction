$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Theorems proved using virtual deduction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( Virtual deduction proof of the left-to-right implication of ~ dftr4 .  A
       transitive class is a subset of its power class.  This proof corresponds
       to the virtual deduction proof of ~ dftr4 without accumulating results.
       (Contributed by Alan Sare, 29-Apr-2011.) $)
    trsspwALT $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 idn1 idn2 trss visset elpw e2bir
      e12 in2 gen11 bi2 e01 in1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUFBAUEKUDUJB
      UDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNRUGABOPQSTUFUKUAUBUC $.
      $( [29-Apr-2011] $)
  $}

  ${
    $d x A $.
    $( Virtual deduction proof of ~ trsspwALT .  This proof is the same as the
       proof of ~ trsspwALT except each virtual deduction symbol is replaced by
       its non-virtual deduction symbol equivalent.  A transitive class is a
       subset of its power class.  (Contributed by Alan Sare, 23-Jul-2011.) $)
    trsspwALT2 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 id idd trss sylsyld elpw syl6ibr
      visset iin2 19.21aiv bi2 mpsyl iin1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUF
      BAUEKUDUJBUDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNOUGABRPQSTUFUKUAUBUC $.
      $( [23-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( Short predicate calculus proof of the left-to-right implication of
       ~ dftr4 .  A transitive class is a subset of its power class.  This
       proof was constructed by applying Metamath's minimize command to the
       proof of ~ trsspwALT2 , which is the virtual deduction proof ~ trsspwALT
       without virtual deductions.  (Contributed by Alan Sare, 30-Apr-2011.) $)
    trsspwALT3 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw cv wcel wss trss visset elpw syl6ibr ssrdv ) ACZBAADZMBEZAFO
      AGONFAOHOABIJKL $.
      $( [30-Apr-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of the right-to-left implication of ~ dftr4 .  A
       class which is a subclass of its power class is transitive.  This proof
       corresponds to the virtual deduction proof of ~ sspwtr without
       accumulating results.  (Contributed by Alan Sare, 2-May-2011.) $)
    sspwtr $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr cv wcel wa wi wal wb dftr2 idn1 idn2 simpr ssel elpwi
      e2 e12 simpl e22 in2 gen12 bi2 e01 in1 ) AADZEZAFZUJBGZCGZHZULAHZIZUKAHZJ
      ZCKBKZLUIURUJBCAMUIUQBCUIUOUPUIUOULAEZUMUPUIUOULUHHZUSUIUIUOUNUTUINUIUOUO
      UNUIUOOZUMUNPSAUHULQTULARSUIUOUOUMVAUMUNUASULAUKQUBUCUDUJURUEUFUG $.
      $( [2-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ sspwtr .  This proof is the same as the
       proof of ~ sspwtr except each virtual deduction symbol is replaced by
       its non-virtual deduction symbol equivalent.  A class which is a
       subclass of its power class is transitive.  (Contributed by Alan Sare,
       3-May-2011.) $)
    sspwtrALT $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr cv wcel wa wi wal wb dftr2 simpr ssel elpwi syl56 idd
      simpl syl6 ee22 iin2 19.21aivv bi2 mpsyl iin1 ) AADZEZAFZUIBGZCGZHZUKAHZI
      ZUJAHZJZCKBKZLUHUQUIBCAMUHUPBCUHUNUOUHUNUKAEZULUOUNUMUHUKUGHURULUMNAUGUKO
      UKAPQUHUNUNULUHUNRULUMSTUKAUJOUAUBUCUIUQUDUEUF $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Short predicate calculus proof of the right-to-left implication of
       ~ dftr4 .  A class which is a subclass of its power class is
       transitive.  This proof was constructed by applying Metamath's minimize
       command to the proof of ~ sspwtrALT , which is the virtual deduction
       proof ~ sspwtr without virtual deductions.  (Contributed by Alan Sare,
       3-May-2011.) $)
    sspwtrALT2 $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss cv wcel wa wal wtr ssel adantld elpwi syl6 simpl a1i ee22
      wi 19.21aivv dftr2 sylibr ) AADZEZBFZCFZGZUEAGZHZUDAGZRZCIBIAJUCUJBCUCUHU
      EAEZUFUIUCUHUEUBGZUKUCUGULUFAUBUEKLUEAMNUHUFRUCUFUGOPUEAUDKQSBCATUA $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ pwtr .  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    pwtrVD $p |- ( Tr A -> Tr ~P A ) $=
      ( vz vy wtr cpw cv wcel wa wi wal wb dftr2 idn1 idn2 simpr e2 elpwi simpl
      wss ssel e22 trss e12 visset elpw e2bir in2 gen12 bi2 e01 in1 ) ADZAEZDZU
      NBFZCFZGZUPUMGZHZUOUMGZIZCJBJZKULVBUNBCUMLULVABCULUSUTULUSUOASZUTULULUSUO
      AGZVCULMULUSUPASZUQVDULUSURVEULUSUSURULUSNZUQUROPUPAQPULUSUSUQVFUQURRPUPA
      UOTUAAUOUBUCUOABUDUEUFUGUHUNVBUIUJUK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( The power class of a transitive class is transitive.  The proof of this
       theorem was automatically generated from ~ pwtrVD using a tools command
       file, translateMWO.cmd , by translating the proof into its non-virtual
       deduction form and minimizing it.  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    pwtr $p |- ( Tr A -> Tr ~P A ) $=
      ( vz vy wtr wel cv cpw wcel wa wi wal wss simpr a1i elpwi syl6 simpl ssel
      ee22 trss syld visset elpw syl6ibr 19.21aivv dftr2 sylibr ) ADZBCEZCFZAGZ
      HZIZBFZUKHZJZCKBKUKDUHUPBCUHUMUNALZUOUHUMUNAHZUQUHUMUJALZUIURUHUMULUSUMUL
      JUHUIULMNUJAOPUMUIJUHUIULQNUJAUNRSAUNTUAUNABUBUCUDUEBCUKUFUG $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    pwtrrVD.1 $e |- A e. _V $.
    $( Virtual deduction proof of ~ pwtrr .  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    pwtrrVD $p |- ( Tr ~P A -> Tr A ) $=
      ( vz vy cpw wtr cv wcel wa wi wal wb dftr2 wss idn1 idn2 simpr pwid trel
      e2 exp3a e120 elpwi simpl ssel e22 in2 gen12 bi2 e01 in1 ) AEZFZAFZUNCGZD
      GZHZUPAHZIZUOAHZJZDKCKZLUMVBUNCDAMUMVACDUMUSUTUMUSUPANZUQUTUMUSUPULHZVCUM
      UMUSURAULHZVDUMOUMUSUSURUMUSPZUQURQTABRUMURVEVDULUPASUAUBUPAUCTUMUSUSUQVF
      UQURUDTUPAUOUEUFUGUHUNVBUIUJUK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    pwtrr.1 $e |- A e. _V $.
    $( A set is transitive if its power set is.  The proof of this theorem was
       automatically generated from ~ pwtrrVD using a tools command file,
       translateMWO.cmd , by translating the proof into its non-virtual
       deduction form and minimizing it.  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    pwtrr $p |- ( Tr ~P A -> Tr A ) $=
      ( vz vy cpw wtr wel cv wcel wa wi wal wss simpr a1i pwid trel exp3a ee120
      id elpwi syl6 simpl ssel ee22 19.21aivv dftr2 sylibr ) AEZFZCDGZDHZAIZJZC
      HZAIZKZDLCLAFUJUQCDUJUNULAMZUKUPUJUNULUIIZURUJUJUNUMAUIIZUSUJTUNUMKUJUKUM
      NOABPUJUMUTUSUIULAQRSULAUAUBUNUKKUJUKUMUCOULAUOUDUEUFCDAUGUH $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Virtual deduction proof of ~ snssiALT .  (Contributed by Alan Sare,
       11-Sep-2011.) $)
    snssiALTVD $p |- ( A e. B -> { A } C_ B ) $=
      ( vx wcel csn wss cv wi wal dfss2 wceq idn1 idn2 elsn e2bi eleq1a e12 in2
      wb gen11 bi2 e01 in1 ) ABDZAEZBFZUFCGZUEDZUGBDZHZCIZSUDUKUFCUEBJUDUJCUDUH
      UIUDUDUHUGAKZUIUDLUDUHUHULUDUHMCANOABUGPQRTUFUKUAUBUC $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( If a class is an element of another class, then its singleton is a
       subclass of that other class.  Alternate proof of ~ snssi .  This
       theorem was automatically generated from ~ snssiALTVD using a
       translation program.  (Contributed by Alan Sare, 11-Sep-2011.) $)
    snssiALT $p |- ( A e. B -> { A } C_ B ) $=
      ( vx wcel cv csn wi wal wss wceq elsn eleq1a syl5bi 19.21aiv dfss2 sylibr
      ) ABDZCEZAFZDZRBDZGZCHSBIQUBCTRAJQUACAKABRLMNCSBOP $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    snsslVD.1 $e |- A e. _V $.
    $( Virtual deduction proof of ~ snssl .  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    snsslVD $p |- ( { A } C_ B -> A e. B ) $=
      ( csn wss wcel idn1 snid ssel2 e10an in1 ) ADZBEZABFZMMALFNMGACHLBAIJK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    snssl.1 $e |- A e. _V $.
    $( If a singleton is a subclass of another class, then the singleton's
       element is an element of that other class.  This theorem is the
       right-to-left implication of the biconditional ~ snss .  The proof of
       this theorem was automatically generated from ~ snsslVD using a tools
       command file, translateMWO.cmd , by translating the proof into its
       non-virtual deduction form and minimizing it.  (Contributed by Alan
       Sare, 25-Aug-2011.) $)
    snssl $p |- ( { A } C_ B -> A e. B ) $=
      ( csn wss wcel snid ssel2 mpan2 ) ADZBEAJFABFACGJBAHI $.
      $( [25-Aug-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ snelpwi .  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    snelpwrVD $p |- ( A e. B -> { A } e. ~P B ) $=
      ( wcel csn cpw cvv wss snex idn1 snssi e1_ elpwg biimprd e01 in1 ) ABCZAD
      ZBECZQFCZPQBGZRAHPPTPIABJKSRTQBFLMNO $.
      $( [25-Aug-2011] $)
  $}

  ${
    $( If a class is contained in another class, then its singleton is
       contained in the power class of that other class.  This theorem is the
       left-to-right implication of the biconditional ~ snelpw .  Unlike
       ~ snelpw , ` A ` may be a proper class.  The proof of this theorem was
       automatically generated from ~ snelpwrVD using a tools command file,
       translateMWO.cmd , by translating the proof into its non-virtual
       deduction form and minimizing it.  (Moved to ~ snelpwi in main set.mm
       and may be deleted by mathbox owner, AS. --NM 10-Sep-2013.) $)
    snelpwrOLD $p |- ( A e. B -> { A } e. ~P B ) $=
      ( snelpwi ) ABC $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x y $.
    $( Virtual deduction proof of ~ unipwr .  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    unipwrVD $p |- A C_ U. ~P A $=
      ( vx cpw cuni cv wcel csn visset snid idn1 snelpwi e1_ elunii e01an ssriv
      in1 ) BAACZDZBEZAFZSRFZSSGZFTUBQFZUASBHITTUCTJSAKLSUBQMNPO $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x y $.
    $( A class is a subclass of the union of its power class.  This theorem is
       the right-to-left subclass lemma of ~ unipw .  The proof of this theorem
       was automatically generated from ~ unipwrVD using a tools command file ,
       translateMWO.cmd , by translating the proof into its non-virtual
       deduction form and minimizing it.  (Contributed by Alan Sare,
       25-Aug-2011.) $)
    unipwr $p |- A C_ U. ~P A $=
      ( vx cpw cuni cv wcel csn visset snid snelpwi elunii sylancr ssriv ) BAAC
      ZDZBEZAFPPGZFQNFPOFPBHIPAJPQNKLM $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ sstrALT2 .  (Contributed by Alan Sare,
       11-Sep-2011.) $)
    sstrALT2VD $p |- ( ( A C_ B /\ B C_ C ) -> A C_ C ) $=
      ( vx wss wa cv wcel wi wal wb dfss2 idn1 simpr e1_ simpl idn2 ssel2 e12an
      in2 gen11 bi2 e01 in1 ) ABEZBCEZFZACEZUHDGZAHZUICHZIZDJZKUGUMUHDACLUGULDU
      GUJUKUGUFUJUIBHZUKUGUGUFUGMZUEUFNOUGUEUJUJUNUGUGUEUOUEUFPOUGUJQABUIRSBCUI
      RSTUAUHUMUBUCUD $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ sstr , transitivity of subclasses, Theorem
       6 of [Suppes] p. 23.  This theorem was automatically generated from
       ~ sstrALT2VD using the command file translate_without_overwriting.cmd .
       It was not minimized because the automated minimization excluding
       duplicates generates a minimized proof which, although not directly
       containing any duplicates, indirectly contains a duplicate.  That is,
       the trace back of the minimized proof contains a duplicate.  This is
       undesirable because some step(s) of the minimized proof use the proven
       theorem.  (Contributed by Alan Sare, 11-Sep-2011.) $)
    sstrALT2 $p |- ( ( A C_ B /\ B C_ C ) -> A C_ C ) $=
      ( vx wss cv wcel wi wal wb wa dfss2 simpr syl simpl idd ssel2 ee12an iin2
      id 19.21aiv bi2 mpsyl ) ACEZDFZAGZUECGZHZDIZJABEZBCEZKZUIUDDACLULUHDULUFU
      GULUKUFUEBGZUGULULUKULTZUJUKMNULUJUFUFUMULULUJUNUJUKONULUFPABUEQRBCUEQRSU
      AUDUIUBUC $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ suctrALT2 .  (Contributed by Alan Sare,
       11-Sep-2011.) $)
    suctrALT2VD $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr csuc cv wcel wa wi wal wb dftr2 wceq wss sssucid idn3 e03 in3
      wo e2 idn1 idn2 simpl trel exp3a e123 ssel eleq2 biimpcd e23 simpr elsuci
      jao e222 in2 gen12 bi2 e01 in1 ) ADZAEZDZVBBFZCFZGZVDVAGZHZVCVAGZIZCJBJZK
      UTVJVBBCVALUTVIBCUTVGVHUTVGVDAGZVHIVDAMZVHIVKVLSZVHUTVGVKVHAVANZUTVGVKVCA
      GZVHAOZUTUTVGVEVKVKVOUTUAUTVGVGVEUTVGUBZVEVFUCTZUTVGVKPUTVEVKVOAVCVDUDUEU
      FAVAVCUGZQRUTVGVLVHVNUTVGVLVOVHVPUTVGVEVLVLVOVRUTVGVLPVLVEVOVDAVCUHUIUJVS
      QRUTVGVFVMUTVGVGVFVQVEVFUKTVDAULTVKVHVLUMUNUOUPVBVJUQURUS $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ suctr .  The sucessor of a transitive class
       is transitive.  This proof was generated automatically from the virtual
       deduction proof ~ suctrALT2VD using the tools command file
       translate_without_overwriting_minimize_excluding_duplicates.cmd .
       (Contributed by Alan Sare, 11-Sep-2011.) $)
    suctrALT2 $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr wel cv csuc wcel wa wi wal wceq wo wss sssucid trel exp3a a1i
      ee03 syl6 adantrd ssel simpl eleq2 simpr elsuci jao ee222 19.21aivv dftr2
      biimpcd sylibr ) ADZBCEZCFZAGZHZIZBFZUPHZJZCKBKUPDUMVABCUMURUOAHZUTJUOALZ
      UTJVBVCMZUTAUPNZUMURVBUSAHZUTAOZUMUNVBVFJUQUMUNVBVFAUSUOPQUAAUPUSUBZSVEUM
      URVCVFUTVGUMURUNVCVFJURUNJUMUNUQUCRVCUNVFUOAUSUDUKTVHSUMURUQVDURUQJUMUNUQ
      UERUOAUFTVBUTVCUGUHUIBCUPUJUL $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Virtual deduction proof of ~ elex2 .  (Contributed by Alan Sare,
       25-Sep-2011.) $)
    elex2VD $p |- ( A e. B -> E. x x e. B ) $=
      ( wcel cv wex wceq wi wal idn1 idn2 eleq1a e12 in2 gen11 elex e1_ e11 in1
      exim ) BCDZAEZCDZAFZUAUBBGZUCHZAIUEAFZUDUAUFAUAUEUCUAUAUEUEUCUAJZUAUEKBCU
      BLMNOUAUAUGUHABCPQUEUCATRS $.
      $( [25-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ elex22 .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    elex22VD $p |- ( ( A e. B /\ A e. C ) -> E. x ( x e. B /\ x e. C ) ) $=
      ( wcel wa cv wex wceq idn1 simpl e1_ elex wal idn2 eleq1a e12 simpr pm3.2
      wi e22 in2 gen11 exim pm2.27 e11 in1 ) BCEZBDEZFZAGZCEZUKDEZFZAHZUJUKBIZA
      HZUQUOTZUOUJUHUQUJUJUHUJJZUHUIKLZABCMLUJUPUNTZANURUJVAAUJUPUNUJUPULUMUNUJ
      UHUPUPULUTUJUPOZBCUKPQUJUIUPUPUMUJUJUIUSUHUIRLVBBDUKPQULUMSUAUBUCUPUNAUDL
      UQUOUEUFUG $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x C $.  $d x A $.
    $( Virtual deduction proof of ~ eqsbc3r .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    eqsbc3rVD $p |- ( A e. B -> ( [ A / x ] C = x <-> C = A ) ) $=
      ( wcel cv wceq wsbc wb idn1 eqsbc3 e1_ eqcom sbcbii idn2 bi1 e12 in2 bi2
      wi e2bi e2bir bi3 e11 in1 ) BCEZDAFZGZABHZDBGZIZUFUIUJTUJUITUKUFUIUJUFUIB
      DGZUJUFUGDGZABHZULIZUIUNULUFUFUOUFJZABDCKLZUFUIUNIZUIUIUNUFUFURUPUHUMABCD
      UGMNLZUFUIOUIUNPQUNULPQBDMZUARUFUJUIUFURUJUNUIUSUFUOUJULUNUQUFUJUJULUFUJO
      UTUBUNULSQUIUNSQRUIUJUCUDUE $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.
    $( Virtual deduction proof of ~ zfregs2 .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    zfregs2VD $p |- ( A =/= (/) -> -. A. x e. A E. y ( y e. A /\ y e. x ) ) $=
      ( c0 wne cv wcel wa wex wral wn wrex wal cin wceq idn1 zfregs rexbii e1bi
      wi incom eqeq1i disj1 alinexa dfrex2 notnot2 notnot1 impbii ralbii notbii
      e1_ in1 ) CDEZBFZCGZUNAFZGZHBIZACJZKZUMURKZKZACJZKZUTUMVAACLZVDUMUOUQKTBM
      ZACLZVEUMCUPNZDOZACLZVGUMUPCNZDOZACLZVJUMUMVMUMPACQUKVLVIACVKVHDUPCUAUBRS
      VIVFACBCUPUCRSVFVAACUOUQBUDRSVAACUESVCUSVBURACVBURURUFURUGUHUIUJSUL $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.  $d x ps $.
    $( Virtual deduction proof of ~ tpid3g .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    tpid3gVD $p |- ( A e. B -> A e. { C , D , A } ) $=
      ( vx wcel ctp cv wceq wex wi wal idn2 w3o cab 3mix3 e2 abid e2bir dftp2
      eleq2i eleq1 biimpd e22 in2 gen11 19.23v e1bi idn1 elex e1_ id e11 in1 )
      ABFZACDAGZFZUOEHZAIZEJZUQKZUTUQUOUSUQKZELVAUOVBEUOUSUQUOUSUSURUPFZUQUOUSM
      ZUOUSURURCIZURDIZUSNZEOZFZVCUOUSVGVIUOUSUSVGVDUSVEVFPQVGERSUPVHURECDATUAS
      USVCUQURAUPUBUCUDUEUFUSUQEUGUHUOUOUTUOUIEABUJUKVAULUMUN $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Virtual deduction proof of ~ en3lplem1 .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    en3lplem1VD $p |- ( ( A e. B /\ B e. C /\ C e. A ) ->
                    ( x = A -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel w3a cv wceq ctp wa wex wi idn1 simp3 e1_ tpid3g idn2 eleq2 biimprd
      e21 pm3.2 e12 elex22 e2 in2 in1 ) CDFZDEFZECFZGZAHZCIZBHZCDEJZFUNULFKBLZM
      UKUMUPUKUMEUOFZEULFZKZUPUKUQUMURUSUKUJUQUKUKUJUKNUHUIUJOPZECCDQPUKUMUMUJU
      RUKUMRUTUMURUJULCESTUAUQURUBUCBEUOULUDUEUFUG $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Virtual deduction proof of ~ en3lplem2 .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    en3lplem2VD $p |- ( ( A e. B /\ B e. C /\ C e. A ) -> ( x e. { A , B , C }
                    -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel w3a cv ctp wa wex wi wceq wo idn3 en3lplem1 e13 in3 eleq2i e2bi
      idn1 3anrot e1bi tprot anbi1i exbii e3bir jao e22 e1bir e3bi w3o cab idn2
      dftp2 abid df-3or e222 in2 in1 ) CDFZDEFZECFZGZAHZCDEIZFZBHZVFFZVHVEFZJZB
      KZLVDVGVLVDVGVECMZVEDMZNZVLLZVEEMZVLLVOVQNZVLVDVGVMVLLVNVLLVPVDVGVMVLVDVD
      VGVMVMVLVDUAZVDVGVMOABCDEPQRVDVGVNVLVDVGVNVHDECIZFZVJJZBKZVLVDVBVCVAGZVGV
      NVNWCVDVDWDVSVAVBVCUBUCVDVGVNOABDECPQVKWBBVIWAVJVFVTVHCDEUDSUEUFUGRVMVLVN
      UHUIVDVGVQVLVDVGVQVHECDIZFZVJJZBKZVLVDVCVAVBGZVGVQVQWHVDVDWIVSVCVAVBUBUJV
      DVGVQOABECDPQWGVKBWFVIVJWEVFVHECDUDSUEUFUKRVDVGVMVNVQULZVRVDVGVEWJAUMZFZW
      JVDVGVGWLVDVGUNVFWKVEACDEUOSTWJAUPTVMVNVQUQTVOVLVQUHURUSUT $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x ph $.
    $( Virtual deduction proof of ~ en3lp .  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    en3lpVD $p |- -. ( A e. B /\ B e. C /\ C e. A ) $=
      ( vy vx ctp c0 wne wceq wo wcel w3a wn pm2.1 df-ne bicomi orbi1i cv con3i
      e1_ mpbi wa wex wral zfregs2 wi wal en3lplem2 19.21aiv df-ral sylibr idn1
      syl noel eleq2 notbid biimprd e10 tpid3g simp3 in1 jaoi ax-mp ) ABCFZGHZV
      DGIZJZABKZBCKZCAKZLZMZVFMZVFJVGVFNVMVEVFVEVMVDGOPQUAVEVLVFVEDRZVDKVNERZKU
      BDUCZEVDUDZMVLEDVDUEVKVQVKVOVDKVPUFZEUGVQVKVREEDABCUHUIVPEVDUJUKSUMVFVLVF
      VJMZVLVFCVDKZMZVSVFVFCGKZMZWAVFULCUNVFWAWCVFVTWBVDGCUOUPUQURVJVTCAABUSSTV
      KVJVHVIVJUTSTVAVBVC $.
      $( [24-Oct-2011] $)
  $}

