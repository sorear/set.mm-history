$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Virtual Deduction transcriptions of textbook proofs
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The following User's Proof is a Natural Deduction Sequent Calculus
       transcription of the Fitch-style Natural Deduction proof of Unit 20
       Excercise 3.a., which is ~sb5 , found in the "Answers to Starred
       Exercises" on page 457 of "Understanding Symbolic Logic", Fifth
       Edition (2008), by Virginia Klenk. The same proof may also be
       interpreted as a Virtual Deduction Hilbert-style axiomatic proof. It
       was completed automatically by the tools program completeusersproof.cmd,
       which invokes Mel O'Cat's mmj2 and Norm Megill's Metamath Proof
       Assistant.  ~ sb5ALT is ~ sb5ALTVD without virtual deductions and
       was automatically derived from ~ sb5ALTVD .
       <HTML> <TABLE>
       <TR> <TD> 1::        <TD> ` |- (. [ y / x ] ph |-. [ y / x ] ph ). `
       <TR> <TD> 2::        <TD> ` |- [ y / x ] x = y `
       <TR> <TD> 3:1,2:     <TD> ` |- (. [ y / x ] ph |-. [ y / x ] ( x = y `
       ` /\ ph ) ). `
       <TR> <TD> 4:3:       <TD> ` |- (. [ y / x ] ph |-.  E. x ( x = y /\ ph `
       ` ) ). `
       <TR> <TD> 5:4:       <TD> ` |- ( [ y / x ] ph -> E. x ( x = y /\ ph ) `
       ` ) `
       <TR> <TD> 6::        <TD> ` |- (. E. x ( x = y /\ ph ) |-. E. x ( x = `
       ` y /\ ph ) ). `
       <TR> <TD> 7::        <TD> ` |- (. E. x ( x = y /\ ph ) , ( x = y /\ ph `
       ` ) |-. ( x = y /\ ph ) ). `
       <TR> <TD> 8:7:       <TD> ` |- (. E. x ( x = y /\ ph ) , ( x = y /\ ph `
       ` ) |-. ph ). `
       <TR> <TD> 9:7:       <TD> ` |- (. E. x ( x = y /\ ph ) , ( x = y /\ ph `
       ` ) |-. x = y ). `
       <TR> <TD> 10:8,9:    <TD> ` |- (. E. x ( x = y /\ ph ) , ( x = y /\ ph `
       ` ) |-. [ y / x ] ph ). `
       <TR> <TD> 101::      <TD> ` |- ( [ y / x ] ph -> A. x [ y / x ] ph ) `
       <TR> <TD> 11:101,10: <TD> ` |- ( E. x ( x = y /\ ph ) -> [ y / x ] ph `
       ` ) `
       <TR> <TD> 12:5,11:   <TD> ` |- ( ( [ y / x ] ph ->  E. x ( x = y /\ ph `
       ` ) ) /\ ( E. x ( x = y /\ ph ) -> [ y / x ] ph ) ) `
       <TR> <TD> qed:12:    <TD> ` |- ( [ y / x ] ph <-> E. x ( x = y /\ ph ) `
       ` ) `
       </TABLE> </HTML> $)
    sb5ALTVD $p |- ( [ y / x ] ph <-> E. x ( x = y /\ ph ) ) $=
      ( cv wsbc wceq wa wex wi wb idn1 equsb1 sban simplbi2com e10 a4sbe e1_
      in1 hbs1 idn2 simpr e2 simpl sbequ1 com12 e22 exinst pm3.2i bi3 imp e0_
      ) ABCDZEZBDULFZAGZBHZIZUPUMIZGUMUPJZUQURUMUPUMUOBULEZUPUMUMUNBULEZUTUMKBC
      LUTVAUMUNABCMNOUOBCPQRUOUMBABCSUPUOAUNUMUPUOUOAUPUOTZUNAUAUBUPUOUOUNVBUNA
      UCUBUNAUMABCUDUEUFUGUHUQURUSUMUPUIUJUK $.
      $( [21-Apr-2013] $)
  $}

  ${
    vk15.4jVD.1 $e |- -. ( E. x -. ph /\ E. x ( ps /\ -. ch ) ) $.
    vk15.4jVD.2 $e |- ( A. x ch -> -. E. x ( th /\ ta ) ) $.
    vk15.4jVD.3 $e |- -. A. x ( ta -> ph ) $.
    $( The following User's Proof is a Natural Deduction Sequent Calculus
       transcription of the Fitch-style Natural Deduction proof of Unit 15
       Excercise 4.f. found in the "Answers to Starred Exercises" on page 442
       of "Understanding Symbolic Logic", Fifth Edition (2008), by Virginia
       Klenk. The same proof may also be interpreted to be a Virtual Deduction
       Hilbert-style axiomatic proof. It was completed automatically by the
       tools program completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and
       Norm Megill's Metamath Proof Assistant.  ~ vk15.4j is ~ vk15.4jVD
       without virtual deductions and was automatically derived
       from ~ vk15.4jVD . Step numbers greater than 25 are additional steps
       necessary for the sequent calculus proof not contained in the
       Fitch-style proof. Otherwise, step i of the User's Proof corresponds to
       step i of the Fitch-style proof.
       <HTML> <TABLE>
       <TR> <TD> h1::               <TD> ` |- -. ( E. x -. ph /\ E. x ( ps /\ `
       ` -. ch ) ) `
       <TR> <TD> h2::               <TD> ` |- ( A. x ch -> -. E. x ( th /\ ta `
       ` ) ) `
       <TR> <TD> h3::               <TD> ` |- -. A. x ( ta -> ph ) `
       <TR> <TD> 4::                <TD> ` |- (. -. E. x -. th |-. -. E. x -. `
       ` th ). `
       <TR> <TD> 5:4:               <TD> ` |- (. -. E. x -. th |-. A. x th ). `
       <TR> <TD> 6:3:               <TD> ` |- E. x ( ta /\ -. ph ) `
       <TR> <TD> 7::                <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. ( ta /\ -. ph ) ). `
       <TR> <TD> 8:7:               <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. ta ). `
       <TR> <TD> 9:7:               <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. -. ph ). `
       <TR> <TD> 10:5:              <TD> ` |- (. -. E. x -. th |-. th ). `
       <TR> <TD> 11:10,8:           <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. ( th /\ ta ) ). `
       <TR> <TD> 12:11:             <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. E. x ( th /\ ta ) ). `
       <TR> <TD> 13:12:             <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. -. -. E. x ( th /\ ta ) ). `
       <TR> <TD> 14:2,13:           <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. -. A. x ch ). `
       <TR> <TD> 140::              <TD> ` |- ( E. x -. th -> A. x E. x -. th `
       ` ) `
       <TR> <TD> 141:140:           <TD> ` |- ( -. E. x -. th -> A. x -. E. x `
       ` -. th ) `
       <TR> <TD> 142::              <TD> ` |- ( A. x ch -> A. x A. x ch ) `
       <TR> <TD> 143:142:           <TD> ` |- ( -. A. x ch -> A. x -. A. x ch `
       ` ) `
       <TR> <TD> 144:6,14,141,143:  <TD> ` |- (. -. E. x -. th |-. -. A. x ch `
       ` ). `
       <TR> <TD> 15:1:              <TD> ` |- ( -. E. x -. ph \/ -. E. x ( ps `
       ` /\ -. ch ) ) `
       <TR> <TD> 16:9:              <TD> ` |- (. -. E. x -. th , ( ta /\ -. `
       ` ph ) |-. E. x -. ph ). `
       <TR> <TD> 161::              <TD> ` |- ( E. x -. ph -> A. x E. x -. ph `
       ` ) `
       <TR> <TD> 162:6,16,141,161:  <TD> ` |- (. -. E. x -. th |-. E. x -. ph `
       ` ). `
       <TR> <TD> 17:162:            <TD> ` |- (. -. E. x -. th |-. -. -. E. x `
       ` -. ph ). `
       <TR> <TD> 18:15,17:          <TD> ` |- (. -. E. x -. th |-. -. E. x ( `
       ` ps /\ -. ch ) ). `
       <TR> <TD> 19:18:             <TD> ` |- (. -. E. x -. th |-. A. x ( ps `
       ` -> ch ) ). `
       <TR> <TD> 20:144:            <TD> ` |- (. -. E. x -. th |-. E. x -. ch `
       ` ). `
       <TR> <TD> 21::               <TD> ` |- (. -. E. x -. th , -. ch |-. -. `
       ` ch ). `
       <TR> <TD> 22:19:             <TD> ` |- (. -. E. x -. th |-. ( ps -> ch `
       ` ) ). `
       <TR> <TD> 23:21,22:          <TD> ` |- (. -. E. x -. th , -. ch |-. -. `
       ` ps ). `
       <TR> <TD> 24:23:             <TD> ` |- (. -. E. x -. th , -. ch |-. E. `
       ` x -. ps ). `
       <TR> <TD> 240::              <TD> ` |- ( E. x -. ps -> A. x E. x -. ps `
       ` ) `
       <TR> <TD> 241:20,24,141,240: <TD> ` |- (. -. E. x -. th |-. E. x -. ps `
       ` ). `
       <TR> <TD> 25:241:            <TD> ` |- (. -. E. x -. th |-. -. A. x ps `
       ` ). `
       <TR> <TD> qed:25:            <TD> ` |- ( -. E. x -. th -> -. A. x ps ) `
       </TABLE> </HTML> $)
    vk15.4jVD $p |- ( -. E. x -. th -> -. A. x ps ) $=
      ( wn wex wal wa wi exanali biimpri e0_ idn1 alex e1_ ax4 idn2 simpl e2
      pm3.2 e12 19.8a notnot1 con3 e02 hbe1 hbn hba1 exinst01 exnal wo pm3.13
      simpr pm2.53 e01 con5i com12 e21 exinst11 biimpi in1 ) DJZFKZJZBFLJZVIBJZ
      FKZVJVICJZVLFVICFLZJZVMFKZVIEAJZMZVOFEANFLJZVRFKZIVTVSEAFOPQZVNDEMZFKZJZN
      VIVRWDJZVOHVIVRWCWEVIVRWBWCVIDVREWBVIDFLZDVIVIWFVIRWFVIDFSPTDFUATVIVRVREV
      IVRUBZEVQUCUDDEUEUFWBFUGUDWCUHUDVNWDUIUJVHFVGFUKULZVNFCFUMULUNVPVOCFUOPTV
      IVMVKVLVIVMVMBCNZVKVIVMUBVIWIFLZWIVIBVMMFKZJZWJVQFKZJZWLUPZVIWNJZWLWMWKMJ
      WOGWMWKUQQVIWMWPVIVRWMFWAVIVRVQWMVIVRVRVQWGEVQURUDVQFUGUDWHVQFUKUNWMUHTWN
      WLUSUTWKWJBCFOVATWIFUATWIVMVKBCUIVBVCVKFUGUDWHVKFUKVDVLVJBFUOVETVF $.
      $( [21-Apr-2013] $)
  $}

  $( The following User's Proof is a Natural Deduction Sequent Calculus
     transcription of the Fitch-style Natural Deduction proof of Theorem 5 of
     Section 14 of [Margaris] p. 59 ( which is ~ notnot2 ). The same proof
     may also be interpreted as a Virtual Deduction Hilbert-style
     axiomatic proof. It was completed automatically by the tools program
     completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm Megill's
     Metamath Proof Assistant.  ~ notnot2ALT is ~ notnot2ALTVD
     without virtual deductions and was automatically derived
     from ~ notnot2ALTVD . Step i of the User's Proof corresponds to
     step i of the Fitch-style proof.
     <HTML> <TABLE>
     <TR> <TD> 1::    <TD> ` |- (. -. -. ph |-. -. -. ph ). `
     <TR> <TD> 2::    <TD> ` |- ( -. -. ph -> ( -. ph -> -. -. -. ph ) ) `
     <TR> <TD> 3:1:   <TD> ` |- (. -. -. ph |-. ( -. ph -> -. -. -. ph ) ). `
     <TR> <TD> 4::    <TD> ` |- ( ( -. ph -> -. -. -. ph ) -> ( -. -. ph -> `
     ` ph ) ) `
     <TR> <TD> 5:3:   <TD> ` |- (. -. -. ph |-. ( -. -. ph -> ph ) ). `
     <TR> <TD> 6:5,1: <TD> ` |- (. -. -. ph |-. ph ). `
     <TR> <TD> qed:6: <TD> ` |- ( -. -. ph -> ph ) `
     </TABLE> </HTML> $)
  notnot2ALTVD $p |- ( -. -. ph -> ph ) $=
    ( wn wi idn1 pm2.21 e1_ ax-3 id e11 in1 ) ABZBZALLACZLALKLBZCZMLLOLDZKNEFAL
    GFPMHIJ $.
    $( [21-Apr-2013] $)

  $( The following User's Proof is a Natural Deduction Sequent Calculus
     transcription of the Fitch-style Natural Deduction proof of Theorem 7 of
     Section 14 of [Margaris] p. 60 ( which is ~ con3 ). The same proof
     may also be interpreted to be a Virtual Deduction Hilbert-style
     axiomatic proof. It was completed automatically by the tools program
     completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm Megill's
     Metamath Proof Assistant.  ~ con3ALT is ~ con3ALTVD
     without virtual deductions and was automatically derived
     from ~ con3ALTVD . Step i of the User's Proof corresponds to
     step i of the Fitch-style proof.
     <HTML> <TABLE>
     <TR> <TD> 1::     <TD> ` |- (. ( ph -> ps ) |-. ( ph -> ps ) ). `
     <TR> <TD> 2::     <TD> ` |- (. ( ph -> ps ) , -. -. ph |-. -. -. ph ). `
     <TR> <TD> 3::     <TD> ` |- ( -. -. ph -> ph ) `
     <TR> <TD> 4:2:    <TD> ` |- (. ( ph -> ps ) , -. -. ph |-. ph ). `
     <TR> <TD> 5:1,4:  <TD> ` |- (. ( ph -> ps ) , -. -. ph |-. ps ). `
     <TR> <TD> 6::     <TD> ` |- ( ps -> -. -. ps ) `
     <TR> <TD> 7:6,5:  <TD> ` |- (. ( ph -> ps ) , -. -. ph |-. -. -. ps ). `
     <TR> <TD> 8:7:    <TD> ` |- (. ( ph -> ps ) |-. ( -. -. ph -> -. -. ps `
     ` ) ). `
     <TR> <TD> 9::     <TD> ` |- ( ( -. -. ph -> -. -. ps ) -> ( -. ps -> `
     ` -. ph ) ) `
     <TR> <TD> 10:8:   <TD> ` |- (. ( ph -> ps ) |-. ( -. ps -> -. ph ) ). `
     <TR> <TD> qed:10: <TD> ` |- ( ( ph -> ps ) -> ( -. ps -> -. ph ) ) `
     </TABLE> </HTML> $)
  con3ALTVD $p |- ( ( ph -> ps ) -> ( -. ps -> -. ph ) ) $=
    ( wi wn idn1 idn2 notnot2 e2 id e12 notnot1 in2 ax-3 e1_ in1 ) ABCZBDZADZCZ
    PRDZQDZCSPTUAPTBUAPPTABPEPTTAPTFAGHPIJBKHLRQMNO $.
    $( [21-Apr-2013] $)

$( (End of Alan Sare's mathbox.) $)


