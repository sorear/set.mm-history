$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Theorems proved using virtual deduction with mmj2 assistance
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pm3.26bi2VD.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    $( Virtual deduction proof of ~ pm3.26bi2 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> h1::        <TD> ` |- ( ph <-> ( ps /\ ch ) ) `
       <TR> <TD> 3:1,?: ~e0_   <TD> ` |- ( ( ps /\ ch ) -> ph )  `
       <TR> <TD> qed:3,?: ~e0_ <TD> ` |- ( ps -> ( ch -> ph ) ) `
       </TABLE> </HTML> $)
    pm3.26bi2VD $p |- ( ps -> ( ch -> ph ) ) $=
      ( wa wi wb bi2 e0_ pm3.3 ) BCEZAFZBCAFFAKGLDAKHIBCAJI $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ 3ornot23 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <DT> 1::            <TD> ` |- (. ( -. ph /\ -. ps ) |-. ( -. ph /\
       -. ps ) ). `
       <TR> <TD> 2::            <TD> ` |- (. ( -. ph /\ -. ps ) , ( ch \/ ph \/
       ps ) |-. ( ch \/ ph \/ ps ) ).                  `
       <TR> <TD> 3:1,?: ~e1_      <TD> ` |- (. ( -. ph /\ -. ps ) |-. -. ph ).
       `
       <TR> <TD> 4:1,?: ~e1_      <TD> ` |- (. ( -. ph /\ -. ps ) |-. -. ps ).
       `
       <TR> <TD> 5:3,4,?: ~e11    <TD> ` |- (. ( -. ph /\ -. ps ) |-. -. ( ph
       \/ ps ) ). `
       <TR> <TD> 6:2,?: ~e2       <TD> ` |- (. ( -. ph /\ -. ps ) , ( ch \/ ph
       \/ ps ) |-. ( ch \/ ( ph \/ ps ) ) ).  `
       <TR> <TD> 7:5,6,?: ~e12    <TD> ` |- (. ( -. ph /\ -. ps ) , ( ch \/ ph
       \/ ps ) |-. ch ). `
       <TR> <TD> 8:7:           <TD> ` |- (. ( -. ph /\ -. ps ) |-. ( ( ch \/
       ph \/ ps ) -> ch ) ).           `
       <TR> <TD> qed:8:         <TD> ` |- ( ( -. ph /\ -. ps ) -> ( ( ch \/ ph
       \/ ps ) -> ch ) ) `
       </TABLE> </HTML> $)
    3ornot23VD $p |- ( ( -. ph /\ -. ps ) -> ( ( ch \/ ph \/ ps ) -> ch ) ) $=
      ( wn wa w3o wi wo idn1 pm3.26 e1_ pm3.27 ioran pm3.26bi2 e11 idn2 3orass
      biimpi e2 orel2 e12 in2 in1 ) ADZBDZEZCABFZCGUFUGCUFABHZDZUGCUHHZCUFUDUEU
      IUFUFUDUFIZUDUEJKUFUFUEUKUDUELKUIUDUEABMNOUFUGUGUJUFUGPUGUJCABQRSUHCTUAUB
      UC $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ orbi1r . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |- (. ( ph <-> ps ) |-. ( ph <-> ps ) ). `
       <TR> <TD> 2::         <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ph ) |-. ( ch
       \/ ph ) ). `
       <TR> <TD> 3:2,?: ~e2    <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ph ) |-. (
       ph \/ ch ) ). `
       <TR> <TD> 4:1,3,?: ~e12 <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ph ) |-. (
       ps \/ ch ) ). `
       <TR> <TD> 5:4,?: ~e2    <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ph ) |-. (
       ch \/ ps ) ). `
       <TR> <TD> 6:5:        <TD> ` |- (. ( ph <-> ps ) |-. ( ( ch \/ ph ) -> (
       ch \/ ps ) ) ). `
       <TR> <TD> 7::         <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ps ) |-. ( ch
       \/ ps ) ). `
       <TR> <TD> 8:7,?: ~e2    <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ps ) |-. (
       ps \/ ch ) ). `
       <TR> <TD> 9:1,8,?: ~e12 <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ps ) |-. (
       ph \/ ch ) ). `
       <TR> <TD> 10:9,?: ~e2   <TD> ` |- (. ( ph <-> ps ) , ( ch \/ ps ) |-. (
       ch \/ ph ) ). `
       <TR> <TD> 11:10:      <TD> ` |- (. ( ph <-> ps ) |-. ( ( ch \/ ps ) -> (
       ch \/ ph ) ) ). `
       <TR> <TD> 12:6,11,?: ~e11 <TD> ` |- (. ( ph <-> ps ) |-. ( ( ch \/ ph )
       <-> ( ch \/ ps ) ) ). `
       <TR> <TD> qed:12:     <TD> ` |- ( ( ph <-> ps ) -> ( ( ch \/ ph ) <-> (
       ch \/ ps ) ) ) `
       </TABLE> </HTML> $)
    orbi1rVD $p |- ( ( ph <-> ps ) -> ( ( ch \/ ph ) <-> ( ch \/ ps ) ) ) $=
      ( wb wo wi idn1 idn2 pm1.4 e2 orbi1 biimpd e12 in2 biimprd bi3 e11 in1 )
      ABDZCAEZCBEZDZSTUAFUATFUBSTUASTBCEZUASSTACEZUCSGZSTTUDSTHCAIJSUDUCABCKZLM
      BCIJNSUATSUAUDTSSUAUCUDUESUAUAUCSUAHCBIJSUDUCUFOMACIJNTUAPQR $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ bitr3 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::           <TD> ` |- (. ( ph <-> ps ) |-. ( ph <-> ps ) ).
       `
       <TR> <TD> 2:1,?: ~e1_     <TD> ` |- (. ( ph <-> ps ) |-. ( ps <-> ph )
       ). `
       <TR> <TD> 3::           <TD> ` |- (. ( ph <-> ps ) , ( ph <-> ch ) |-. (
       ph <-> ch ) ). `
       <TR> <TD> 4:3,?: ~e2      <TD> ` |- (. ( ph <-> ps ) , ( ph <-> ch ) |-.
       ( ch <-> ph ) ). `
       <TR> <TD> 5:2,4,?: ~e12   <TD> ` |- (. ( ph <-> ps ) , ( ph <-> ch ) |-.
       ( ps <-> ch ) ). `
       <TR> <TD> 6:5:          <TD> ` |- (. ( ph <-> ps ) |-. ( ( ph <-> ch )
       -> ( ps <-> ch ) ) ). `
       <TR> <TD> qed:6:        <TD> ` |- ( ( ph <-> ps ) -> ( ( ph <-> ch ) ->
       ( ps <-> ch ) ) ) `
       </TABLE> </HTML> $)
    bitr3VD $p |- ( ( ph <-> ps ) -> ( ( ph <-> ch ) -> ( ps <-> ch ) ) ) $=
      ( wb wi idn1 id bicomd e1_ idn2 e2 biantr ex e12 in2 in1 ) ABDZACDZBCDZEQ
      RSQBADZRCADZSQQTQFQABQGHIQRRUAQRJRACRGHKTUASBACLMNOP $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ 3orbi123 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\ (
       ta <-> et ) )  |-. ( ( ph <-> ps ) /\ ( ch <-> th ) /\ ( ta <-> et ) )
       ).  `
       <TR> <TD> 2:1,?: ~e1_   <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ph <-> ps ) ). `
       <TR> <TD> 3:1,?: ~e1_   <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ch <-> th ) ). `
       <TR> <TD> 4:1,?: ~e1_   <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ta <-> et ) ). `
       <TR> <TD> 5:2,3,?: ~e11 <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ( ph \/ ch ) <-> ( ps \/ th ) ) ). `
       <TR> <TD> 6:5,4,?: ~e11 <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ( ( ph \/ ch ) \/ ta )  <-> ( ( ps \/ th ) \/ et )
       ) ). `
       <TR> <TD> 7:?:        <TD> ` |- ( ( ( ph \/ ch ) \/ ta ) <-> ( ph \/ ch
       \/ ta ) ) `
       <TR> <TD> 8:6,7,?: ~e10 <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) |-. ( ( ph \/ ch \/ ta )  <-> ( ( ps \/ th ) \/ et ) )
       ). `
       <TR> <TD> 9:?:        <TD> ` |- ( ( ( ps \/ th ) \/ et ) <-> ( ps \/ th
       \/ et ) ) `
       <TR> <TD> 10:8,9,?: ~e10 <TD> ` |- (. ( ( ph <-> ps ) /\ ( ch <-> th )
       /\ ( ta <-> et ) ) |-. ( ( ph \/ ch \/ ta ) <-> ( ps \/ th \/ et ) ) ).
       `
       <TR> <TD> qed:10:       <TD> ` |- ( ( ( ph <-> ps ) /\ ( ch <-> th ) /\
       ( ta <-> et ) ) -> ( ( ph \/ ch \/ ta ) <-> ( ps \/ th \/ et ) ) ) `
       </TABLE> </HTML> $)
    3orbi123VD $p |- ( ( ( ph <-> ps ) /\ ( ch <-> th ) /\ ( ta <-> et ) ) ->
                   ( ( ph \/ ch \/ ta ) <-> ( ps \/ th \/ et ) ) ) $=
      ( wb w3a w3o wo idn1 3simp1 e1_ 3simp2 pm4.39 ex e11 3simp3 df-3or
      bicomi bitr3 com12 e10 bitr in1 ) ABGZCDGZEFGZHZACEIZBDFIZGZUIUJBDJZFJZGZ
      UNUKGZULUIACJZEJZUNGZURUJGZUOUIUQUMGZUHUSUIUFUGVAUIUIUFUIKZUFUGUHLMUIUIUG
      VBUFUGUHNMUFUGVAACBDOPQUIUIUHVBUFUGUHRMVAUHUSUQEUMFOPQUJURACESTUTUSUOURUJ
      UNUAUBUCUKUNBDFSTUOUPULUJUNUKUDPUCUE $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ sbc3org . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::           <TD> ` |- (. A e. B |-. A e. B ). `
       <TR> <TD> 2:1,?: ~e1_     <TD> ` |- (. A e. B |-. ( [ A / x ] ( ( ph \/
       ps ) \/ ch ) <-> ( [ A / x ] ( ph \/ ps ) \/ [ A / x ] ch ) ) ). `
       <TR> <TD> 3::           <TD> ` |- ( ( ( ph \/ ps ) \/ ch ) <-> ( ph \/
       ps \/ ch ) ) `
       <TR> <TD> 32:3:         <TD> ` |- A. x ( ( ( ph \/ ps ) \/ ch ) <-> ( ph
       \/ ps \/ ch ) ) `
       <TR> <TD> 33:1,32,?: ~e10 <TD> ` |- (. A e. B |-. [ A / x ] ( ( ( ph \/
       ps ) \/ ch ) <-> ( ph \/ ps \/ ch ) ) ). `
       <TR> <TD> 4:1,33,?: ~e11  <TD> ` |- (. A e. B |-. ( [ A / x ] ( ( ph \/
       ps ) \/ ch ) <-> [ A / x ] ( ph \/ ps \/ ch ) ) ). `
       <TR> <TD> 5:2,4,?: ~e11   <TD> ` |- (. A e. B |-. ( [ A / x ] ( ph \/ ps
       \/ ch ) <-> ( [ A / x ] ( ph \/ ps ) \/ [ A / x ] ch ) ) ). `
       <TR> <TD> 6:1,?: ~e1_     <TD> ` |- (. A e. B |-. ( [ A / x ] ( ph \/ ps
       ) <-> ( [ A / x ] ph \/ [ A / x ] ps ) ) ). `
       <TR> <TD> 7:6,?: ~e1_     <TD> ` |- (. A e. B |-. ( ( [ A / x ] ( ph \/
       ps ) \/ [ A / x ] ch ) <-> ( ( [ A / x ] ph \/ [ A / x ] ps ) \/ [ A / x
       ] ch ) ) ). `
       <TR> <TD> 8:5,7,?: ~e11   <TD> ` |- (. A e. B |-. ( [ A / x ] ( ph \/ ps
       \/ ch ) <-> ( ( [ A / x ] ph \/ [ A / x ] ps ) \/ [ A / x ] ch ) ) ). `
       <TR> <TD> 9:?:          <TD> ` |- ( ( ( [ A / x ] ph \/ [ A / x ] ps )
       \/ [ A / x ] ch ) <-> ( [ A / x ] ph \/ [ A / x ] ps \/ [ A / x ] ch ) )
       `
       <TR> <TD> 10:8,9,?: ~e10  <TD> ` |- (. A e. B |-. ( [ A / x ] ( ph \/ ps
       \/ ch ) <-> ( [ A / x ] ph \/ [ A / x ] ps \/ [ A / x ] ch ) ) ). `
       <TR> <TD> qed:10:       <TD> ` |- ( A e. B -> ( [ A / x ] ( ph \/ ps \/
       ch ) <-> ( [ A / x ] ph \/ [ A / x ] ps \/ [ A / x ] ch ) ) ) `
       </TABLE> </HTML> $)
    sbc3orgVD $p |- ( A e. B -> ( [ A / x ] ( ph \/ ps \/ ch ) <->
                  ( [ A / x ] ph \/ [ A / x ] ps \/ [ A / x ] ch ) ) ) $=
      ( wcel w3o wsbc wb wo idn1 sbcorg e1_ wal df-3or bicomi ax-gen a4sbc e10
      sbcbidig biimpd e11 bitr3 com12 orbi1 bibi1 biimprd in1 ) EFGZABCHZDEIZAD
      EIZBDEIZCDEIZHZJZUJULUMUNKZUOKZJZUSUPJZUQUJULABKZDEIZUOKZJZVDUSJZUTUJVBCK
      ZDEIZVDJZVHULJZVEUJUJVIUJLZVBCDEFMNUJUJVGUKJZDEIZVJVKUJUJVLDOVMVKVLDUKVGA
      BCPQRVLDEFSTUJVMVJVGUKDEFUAUBUCVJVIVEVHULVDUDUEUCUJVCURJZVFUJUJVNVKABDEFM
      NVCURUOUFNVEUTVFULVDUSUGUHUCUPUSUMUNUOPQUTUQVAULUSUPUGUHTUI $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d x ps $.  $d x ch $.
    $( Virtual deduction proof of ~ 19.21a3con13v . The following user's
       proof is completed by invoking mmj2's unify command and using mmj2's
       StepSelector to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::            <TD> ` |- (. ( ph -> A. x ph ) |-. ( ph -> A. x
       ph ) ). `
       <TR> <TD> 2::            <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph /\
       ch ) |-. ( ps /\ ph /\ ch ) ). `
       <TR> <TD> 3:2,?: ~e2       <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. ps ). `
       <TR> <TD> 4:2,?: ~e2       <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. ph ). `
       <TR> <TD> 5:2,?: ~e2       <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. ch ). `
       <TR> <TD> 6:1,4,?: ~e12    <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. A. x ph ). `
       <TR> <TD> 7:3,?: ~e2       <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. A. x ps ). `
       <TR> <TD> 8:5,?: ~e2       <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. A. x ch ). `
       <TR> <TD> 9:7,6,8,?: ~e222 <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. ( A. x ps /\ A. x ph /\ A. x ch ) ). `
       <TR> <TD> 10:9,?: ~e2      <TD> ` |- (. ( ph -> A. x ph ) , ( ps /\ ph
       /\ ch ) |-. A. x ( ps /\ ph /\ ch ) ). `
       <TR> <TD> 11:10:in2      <TD> ` |- (. ( ph -> A. x ph ) |-. ( ( ps /\ ph
       /\ ch ) -> A. x ( ps /\ ph /\ ch ) ) ). `
       <TR> <TD> qed:11:in1     <TD> ` |- ( ( ph -> A. x ph ) -> ( ( ps /\ ph
       /\ ch ) -> A. x ( ps /\ ph /\ ch ) ) ) `
       </TABLE> </HTML> $)
    19.21a3con13vVD $p |- ( ( ph -> A. x ph ) ->
                        ( ( ps /\ ph /\ ch ) -> A. x ( ps /\ ph /\ ch ) ) ) $=
      ( wal wi w3a idn2 3simp1 e2 ax-17 idn1 3simp2 id e12 3simp3 pm3.2an3
      e222 19.26-3an biimpri in2 in1 ) AADEZFZBACGZUEDEZFUDUEUFUDUEBDEZUCCDEZGZ
      UFUDUEUGUCUHUIUDUEBUGUDUEUEBUDUEHZBACIJBDKJUDUDUEAUCUDLUDUEUEAUJBACMJUDNO
      UDUECUHUDUEUECUJBACPJCDKJUGUCUHQRUFUIBACDSTJUAUB $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ exbir . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) |-.
       ( ( ph /\ ps ) -> ( ch <-> th ) ) ). `
       <TR> <TD> 2::         <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) , (
       ph /\ ps ) |-. ( ph /\ ps ) ). `
       <TR> <TD> 3::         <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) , (
       ph /\ ps ) , th |-. th ). `
       <TR> <TD> 5:1,2,?: ~e12 <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) ,
       ( ph /\ ps )  |-. ( ch <-> th ) ). `
       <TR> <TD> 6:3,5,?: ~e32 <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) ,
       ( ph /\ ps ) , th |-. ch ). `
       <TR> <TD> 7:6:        <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) , (
       ph /\ ps ) |-. ( th -> ch ) ). `
       <TR> <TD> 8:7:        <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) ) |-.
       ( ( ph /\ ps ) -> ( th -> ch ) ) ). `
       <TR> <TD> 9:8,?: ~e1_   <TD> ` |- (. ( ( ph /\ ps ) -> ( ch <-> th ) )
       |-. ( ph -> ( ps -> ( th -> ch ) ) ) ). `
       <TR> <TD> qed:9:      <TD> ` |- ( ( ( ph /\ ps ) -> ( ch <-> th ) ) -> (
       ph -> ( ps -> ( th -> ch ) ) ) ) `
       </TABLE> </HTML> $)
    exbirVD $p |- ( ( ( ph /\ ps ) -> ( ch <-> th ) ) ->
                ( ph -> ( ps -> ( th -> ch ) ) ) ) $=
      ( wa wb wi idn3 idn1 idn2 id e12 bi2 com12 e32 in3 in2 pm3.3 e1_ in1 ) AB
      EZCDFZGZABDCGZGGZUCUAUDGUEUCUAUDUCUADCUCUADDUBCUCUADHUCUCUAUAUBUCIUCUAJUC
      KLUBDCCDMNOPQABUDRST $.
      $( [13-Dec-2011] $)
  $}

  ${
    exbiriVD.1 $e |- ( ( ph /\ ps ) -> ( ch <-> th ) ) $.
    $( Virtual deduction proof of ~ exbiri . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> h1::        <TD> ` |- ( ( ph /\ ps ) -> ( ch <-> th ) ) `
       <TR> <TD> 2::         <TD> ` |- (. ph |-. ph ). `
       <TR> <TD> 3::         <TD> ` |- (. ph , ps |-. ps ). `
       <TR> <TD> 4::         <TD> ` |- (. ph , ps , th |-. th ). `
       <TR> <TD> 5:2,1,?: ~e10 <TD> ` |- (. ph |-. ( ps -> ( ch <-> th ) ) ). `
       <TR> <TD> 6:3,5,?: ~e21 <TD> ` |- (. ph , ps |-. ( ch <-> th ) ). `
       <TR> <TD> 7:4,6,?: ~e32 <TD> ` |- (. ph , ps , th |-. ch ). `
       <TR> <TD> 8:7:        <TD> ` |- (. ph , ps |-. ( th -> ch ) ). `
       <TR> <TD> 9:8:        <TD> ` |- (. ph |-. ( ps -> ( th -> ch ) ) ). `
       <TR> <TD> qed:9:      <TD> ` |- ( ph -> ( ps -> ( th -> ch ) ) ) `
       </TABLE> </HTML> $)
    exbiriVD $p |- ( ph -> ( ps -> ( th -> ch ) ) ) $=
      ( wi wb idn3 idn2 wa idn1 pm3.3 com12 e10 pm2.27 e21 bi2 e32 in3 in2 in1
      ) ABDCFZFABUBABDCABDDCDGZCABDHABBBUCFZUCABIAAABJUCFZUDAKEUEAUDABUCLMNBUCO
      PUCDCCDQMRSTUA $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d A y $.  $d B x $.  $d D x y $.
    $( Virtual deduction proof of ~ ra4sbc2 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::            <TD> ` |- (. A e. B |-. A e. B ). `
       <TR> <TD> 2::            <TD> ` |- (. A e. B , C e. D |-. C e. D ). `
       <TR> <TD> 3::            <TD> ` |- (. A e. B , C e. D , A. x e. B A. y
       e. D ph |-. A. x e. B A. y e. D ph ). `
       <TR> <TD> 4:1,3,?: ~e13    <TD> ` |- (. A e. B , C e. D , A. x e. B A. y
       e. D ph |-. [ A / x ] A. y e. D ph ). `
       <TR> <TD> 5:1,4,?: ~e13    <TD> ` |- (. A e. B , C e. D , A. x e. B A. y
       e. D ph |-. A. y e. D [ A / x ] ph ). `
       <TR> <TD> 6:2,5,?: ~e23    <TD> ` |- (. A e. B , C e. D , A. x e. B A. y
       e. D ph |-. [ C / y ] [ A / x ] ph ). `
       <TR> <TD> 7:6:           <TD> ` |- (. A e. B , C e. D |-. ( A. x e. B A.
       y e. D ph -> [ C / y ] [ A / x ] ph ) ). `
       <TR> <TD> 8:7:           <TD> ` |- (. A e. B |-. ( C e. D -> ( A. x e. B
       A. y e. D ph -> [ C / y ] [ A / x ] ph ) ) ). `
       <TR> <TD> qed:8:         <TD> ` |- ( A e. B -> ( C e. D -> ( A. x e. B
       A. y e. D ph -> [ C / y ] [ A / x ] ph ) ) ) `
       </TABLE> </HTML> $)
    ra4sbc2VD $p |- ( A e. B -> ( C e. D -> ( A. x e. B A. y e. D ph ->
                  [ C / y ] [ A / x ] ph ) ) ) $=
      ( wcel wral wsbc wi idn2 idn1 idn3 ra4sbc e13 sbcralg biimpd e23 in3 in2
      in1 ) DEHZFGHZACGIZBEIZABDJZCFJZKZKUCUDUIUCUDUFUHUCUDUDUFUGCGIZUHUCUDLUCU
      CUDUFUEBDJZUJUCMZUCUCUDUFUFUKULUCUDUFNUEBDEOPUCUKUJABCDGEQRPUGCFGOSTUAUB
      $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ 3impexp . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::                <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> th )
       |-. ( ( ph /\ ps /\ ch ) -> th ) ).  `
       <TR> <TD> 2::                <TD> ` |- ( ( ph /\ ps /\ ch ) <-> ( ( ph
       /\ ps ) /\ ch ) )  `
       <TR> <TD> 3:1,2,?: ~e10        <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> th )
       |-. ( ( ( ph /\ ps ) /\ ch ) -> th ) ). `
       <TR> <TD> 4:3,?: ~e1_          <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> th )
       |-. ( ( ph /\ ps ) -> ( ch -> th ) ) ). `
       <TR> <TD> 5:4,?: ~e1_          <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> th )
       |-. ( ph -> ( ps -> ( ch -> th ) ) ) ). `
       <TR> <TD> 6:5:               <TD> ` |- ( ( ( ph /\ ps /\ ch ) -> th ) ->
       ( ph -> ( ps -> ( ch -> th ) ) ) ) `
       <TR> <TD> 7::                <TD> ` |- (. ( ph -> ( ps -> ( ch -> th ) )
       ) |-. ( ph -> ( ps -> ( ch -> th ) ) ) ). `
       <TR> <TD> 8:7,?: ~e1_          <TD> ` |- (. ( ph -> ( ps -> ( ch -> th )
       ) ) |-. ( ( ph /\ ps ) -> ( ch -> th ) ) ). `
       <TR> <TD> 9:8,?: ~e1_          <TD> ` |- (. ( ph -> ( ps -> ( ch -> th )
       ) ) |-. ( ( ( ph /\ ps ) /\ ch ) -> th ) ). `
       <TR> <TD> 10:2,9,?: ~e01       <TD> ` |- (. ( ph -> ( ps -> ( ch -> th )
       ) ) |-. ( ( ph /\ ps /\ ch ) -> th ) ). `
       <TR> <TD> 11:10:             <TD> ` |- ( ( ph -> ( ps -> ( ch -> th ) )
       ) -> ( ( ph /\ ps /\ ch ) -> th ) ) `
       <TR> <TD> qed:6,11,?: ~e00     <TD> ` |- ( ( ( ph /\ ps /\ ch ) -> th )
       <-> ( ph -> ( ps -> ( ch -> th ) ) ) ) `
       </TABLE> </HTML> $)
    3impexpVD $p |- ( ( ( ph /\ ps /\ ch ) -> th ) <->
                  ( ph -> ( ps -> ( ch -> th ) ) ) ) $=
      ( w3a wi wb wa idn1 df-3an imbi1 biimpcd e10 pm3.3 e1_ in1 pm3.31
      biimprd e01 bi3 e00 ) ABCEZDFZABCDFZFFZFUEUCFUCUEGUCUEUCABHZUDFZUEUCUFCHZ
      DFZUGUCUCUBUHGZUIUCIABCJZUJUCUIUBUHDKZLMUFCDNOABUDNOPUEUCUJUEUIUCUKUEUGUI
      UEUEUGUEIABUDQOUFCDQOUJUCUIULRSPUCUETUA $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ 3impexpbicom . The following user's proof
       is completed by invoking mmj2's unify command and using mmj2's
       StepSelector to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::                <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> ( th
       <-> ta ) ) |-. ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) ). `
       <TR> <TD> 2::                <TD> ` |- ( ( th <-> ta ) <-> ( ta <-> th )
       ) `
       <TR> <TD> 3:1,2,?: ~e10        <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> ( th
       <-> ta ) ) |-. ( ( ph /\ ps /\ ch ) -> ( ta <-> th ) ) ). `
       <TR> <TD> 4:3,?: ~e1_          <TD> ` |- (. ( ( ph /\ ps /\ ch ) -> ( th
       <-> ta ) ) |-. ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ). `
       <TR> <TD> 5:4:               <TD> ` |- ( ( ( ph /\ ps /\ ch ) -> ( th
       <-> ta ) ) -> ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ) `
       <TR> <TD> 6::                <TD> ` |- (. ( ph -> ( ps -> ( ch -> ( ta
       <-> th ) ) ) ) |-. ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ). `
       <TR> <TD> 7:6,?: ~e1_          <TD> ` |- (. ( ph -> ( ps -> ( ch -> ( ta
       <-> th ) ) ) ) |-. ( ( ph /\ ps /\ ch ) -> ( ta <-> th ) ) ). `
       <TR> <TD> 8:7,2,?: ~e10        <TD> ` |- (. ( ph -> ( ps -> ( ch -> ( ta
       <-> th ) ) ) ) |-. ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) ). `
       <TR> <TD> 9:8:               <TD> ` |- ( ( ph -> ( ps -> ( ch -> ( ta
       <-> th ) ) ) ) -> ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) ) `
       <TR> <TD> qed:5,9,?: ~e00      <TD> ` |- ( ( ( ph /\ ps /\ ch ) -> ( th
       <-> ta ) ) <-> ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ) `
       </TABLE> </HTML> $)
    3impexpbicomVD $p |- ( ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) <->
                       ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) ) $=
      ( w3a wb wi idn1 bicom imbi2 biimpcd e10 3impexp biimpi e1_ in1 biimpri
      biimprcd bi3 e00 ) ABCFZDEGZHZABCEDGZHHHZHUFUDHUDUFGUDUFUDUBUEHZUFUDUDUCU
      EGZUGUDIDEJZUHUDUGUCUEUBKZLMUGUFABCUENZOPQUFUDUFUGUHUDUFUFUGUFIUGUFUKRPUI
      UHUDUGUJSMQUDUFTUA $.
      $( [31-Dec-2011] $)
  $}

  ${
    3impexpbicomiVD.1 $e |- ( ( ph /\ ps /\ ch ) -> ( th <-> ta ) ) $.
    $( Virtual deduction proof of ~ 3impexpbicomi . The following user's proof
       is completed by invoking mmj2's unify command and using mmj2's
       StepSelector to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> h1::               <TD> ` |- ( ( ph /\ ps /\ ch ) -> ( th <->
       ta ) ) `
       <TR> <TD> qed:1,?: ~e0_        <TD> ` |- ( ph -> ( ps -> ( ch -> ( ta
       <-> th ) ) ) ) `
       </TABLE> </HTML> $)
    3impexpbicomiVD $p |- ( ph -> ( ps -> ( ch -> ( ta <-> th ) ) ) ) $=
      ( w3a wb wi 3impexpbicom biimpi e0_ ) ABCGDEHIZABCEDHIIIZFMNABCDEJKL $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d A y $.  $d x y $.
    $( Virtual deduction proof of ~ sbcoreleleq . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::            <TD> ` |- (. A e. B |-. A e. B ). `
       <TR> <TD> 2:1,?: ~e1_      <TD> ` |- (. A e. B |-. ( [ A / y ] x e. y
       <-> x e. A ) ). `
       <TR> <TD> 3:1,?: ~e1_      <TD> ` |- (. A e. B |-. ( [ A / y ] y e. x
       <-> A e. x ) ). `
       <TR> <TD> 4:1,?: ~e1_      <TD> ` |- (. A e. B |-. ( [ A / y ] x = y <->
       x = A ) ). `
       <TR> <TD> 5:2,3,4,?: ~e111 <TD> ` |- (. A e. B |-. ( ( x e. A \/ A e. x
       \/ x = A ) <-> ( [ A / y ] x e. y \/ [ A / y ] y e. x \/ [ A / y ] x = y
       ) ) ). `
       <TR> <TD> 6:1,?: ~e1_      <TD> ` |- (. A e. B |-. ( [ A / y ] ( x e. y
       \/ y e. x \/ x = y ) <-> ( [ A / y ] x e. y \/ [ A / y ] y e. x \/ [ A /
       y ] x = y ) ) ). `
       <TR> <TD> 7:5,6: ~e11      <TD> ` |- (. A e. B |-. ( [ A / y ] ( x e. y
       \/ y e. x \/ x = y ) <-> ( x e. A \/ A e. x \/ x = A ) ) ).  `
       <TR> <TD> qed:7:         <TD> ` |- ( A e. B -> ( [ A / y ] ( x e. y \/ y
       e. x \/ x = y ) <-> ( x e. A \/ A e. x \/ x = A ) ) )  `
       </TABLE> </HTML> $)
    sbcoreleleqVD $p |- ( A e. B -> ( [ A / y ] ( x e. y \/ y e. x \/ x = y )
        <->
                      ( x e. A \/ A e. x \/ x = A ) ) ) $=
      ( wcel cv wceq w3o wsbc wb idn1 sbcel2gv e1_ sbcel1gv eqsbc3r 3orbi123
      3impexpbicomi e111 sbc3org biantr expcom e11 in1 ) CDEZAFZBFZEZUFUEEZUEUF
      GZHBCIZUECEZCUEEZUECGZHZJZUDUNUGBCIZUHBCIZUIBCIZHZJZUJUSJZUOUDUPUKJZUQULJ
      ZURUMJZUTUDUDVBUDKZBUECDLMUDUDVCVEBCUEDNMUDUDVDVEBCDUEOMVBVCVDUSUNUPUKUQU
      LURUMPQRUDUDVAVEUGUHUIBCDSMVAUTUOUJUSUNTUAUBUC $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d A y $.  $d B x $.  $d x y $.
    $( Virtual deduction proof of ~ hbra2 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's
       StepSelector to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::              <TD> ` |- ( A. y e. B A. x e. A ph ->
       A. y A. y e. B A. x e. A ph ) `
       <TR> <TD> 2::              <TD> ` |- ( A. x e. A A. y e. B ph <->
       A. y e. B A. x e. A ph ) `
       <TR> <TD> 3:1,2,?: ~ e00   <TD> ` |- ( A. x e. A A. y e. B ph ->
       A. y A. y e. B A. x e. A ph ) `
       <TR> <TD> 4:2:             <TD> ` |- A. y ( A. x e. A A. y e. B ph <->
       A. y e. B A. x e. A ph ) `
       <TR> <TD> 5:4,?: ~ e0_     <TD> ` |- ( A. y A. x e. A A. y e. B ph <->
       A. y A. y e. B A. x e. A ph ) `
       <TR> <TD> qed:3,5,?: ~ e00 <TD> ` |- ( A. x e. A A. y e. B ph ->
       A. y A. x e. A A. y e. B ph ) `
       </HTML> </TABLE> $)
    hbra2VD $p |- ( A. x e. A A. y e. B ph -> A. y A. x e. A A. y e. B ph ) $=
      ( wral wal wi wb hbra1 ralcom imbi1 biimprcd e00 ax-gen albi e0_ imbi2 )
      ACEFBDFZABDFZCEFZCGZHZSCGZUBIZSUDHZUAUBHZSUAIZUCTCEJABCDEKZUHUCUGSUAUBLMN
      UHCGUEUHCUIOSUACPQUEUFUCUDUBSRMN $.
      $( [31-Dec-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Virtual deduction proof of ~ tratrb . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::                <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. ( Tr A /\ A. x e. A A. y
       e. A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) ). `
       <TR> <TD> 2:1,?: ~e1_          <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. Tr A ). `
       <TR> <TD> 3:1,?: ~e1_          <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. A. x e. A A. y e. A ( x
       e. y \/ y e. x \/ x = y ) ). `
       <TR> <TD> 4:1,?: ~e1_          <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. B e. A ). `
       <TR> <TD> 5::                <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. ( x
       e. y /\ y e. B ) ). `
       <TR> <TD> 6:5,?: ~e2           <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. x
       e. y ). `
       <TR> <TD> 7:5,?: ~e2           <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. y
       e. B ). `
       <TR> <TD> 8:2,7,4,?: ~e121     <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. y
       e. A ). `
       <TR> <TD> 9:2,6,8,?: ~e122     <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. x
       e. A ).  `
       <TR> <TD> 10::               <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) , B e.
       x |-. B e. x ). `
       <TR> <TD> 11:6,7,10,?: ~e223   <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) , B
       e. x |-. ( x e. y /\ y e. B /\ B e. x ) ). `
       <TR> <TD> 12:11:             <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. ( B
       e. x -> ( x e. y /\ y e. B /\ B e. x ) ) ). `
       <TR> <TD> 13::               <TD> ` |- -. ( x e. y /\ y e. B /\ B e. x )
       `
       <TR> <TD> 14:12,13,?: ~e20     <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-.
       -. B e. x ). `
       <TR> <TD> 15::               <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) , x = B
       |-. x = B ). `
       <TR> <TD> 16:7,15,?: ~e23      <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) , x =
       B |-. y e. x ). `
       <TR> <TD> 17:6,16,?: ~e23      <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) , x =
       B |-. ( x e. y /\ y e. x ) ). `
       <TR> <TD> 18:17:             <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. ( x
       = B -> ( x e. y /\ y e. x ) ) ). `
       <TR> <TD> 19::               <TD> ` |- -. ( x e. y /\ y e. x ) `
       <TR> <TD> 20:18,19,?: ~e20     <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-.
       -. x = B ). `
       <TR> <TD> 21:3,?: ~e1_         <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. A. y e. A A. x e. A ( x
       e. y \/ y e. x \/ x = y ) ). `
       <TR> <TD> 22:21,9,4,?: ~e121   <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. [
       x / x ] [ B / y ] ( x e. y \/ y e. x \/ x = y ) ). `
       <TR> <TD> 23:22,?: ~e2         <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. [
       B / y ] ( x e. y \/ y e. x \/ x = y ) ). `
       <TR> <TD> 24:4,23,?: ~e12      <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. (
       x e. B \/ B e. x \/ x = B ) ). `
       <TR> <TD> 25:14,20,24,?: ~e222 <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) , ( x e. y /\ y e. B ) |-. x
       e. B ). `
       <TR> <TD> 26:25:             <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. ( ( x e. y /\ y e. B ) ->
       x e. B ) ). `
       <TR> <TD> 27::               <TD> ` |- ( A. x e. A A. y e. A ( x e. y \/
       y e. x \/ x = y ) -> A. y A. x e. A A. y e. A ( x e. y \/ y e. x \/ x =
       y ) ) `
       <TR> <TD> 28:27,?: ~e0_        <TD> ` |- ( ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) -> A. y ( Tr A /\ A. x e. A A.
       y e. A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) ) `
       <TR> <TD> 29:28,26:          <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. A. y ( ( x e. y /\ y e. B
       ) -> x e. B ) ). `
       <TR> <TD> 30::               <TD> ` |- ( A. x e. A A. y e. A ( x e. y \/
       y e. x \/ x = y ) -> A. x A. x e. A A. y e. A ( x e. y \/ y e. x \/ x =
       y ) ) `
       <TR> <TD> 31:30,?: ~e0_        <TD> ` |- ( ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) -> A. x ( Tr A /\ A. x e. A A.
       y e. A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) ) `
       <TR> <TD> 32:31,29:          <TD> ` |- (. ( Tr A /\ A. x e. A A. y e. A
       ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. A. x A. y ( ( x e. y /\ y
       e. B ) -> x e. B ) ). `
       <TR> <TD> 33:32,?: ~e1_        <TD> ` |- (. ( Tr A /\ A. x e. A A. y e.
       A ( x e. y \/ y e. x \/ x = y ) /\ B e. A ) |-. Tr B ). `
       <TR> <TD> qed:33:            <TD> ` |- ( ( Tr A /\ A. x e. A A. y e. A (
       x e. y \/ y e. x \/ x = y ) /\ B e. A ) -> Tr B ) `
       </TABLE> </HTML> $)
    tratrbVD $p |- ( ( Tr A /\ A. x e. A A. y e. A ( x e. y \/ y e. x \/ x = y
        )
                 /\ B e. A ) -> Tr B ) $=
      ( wtr cv wcel wceq w3o wral w3a wa wi wal hbra1 19.21a3con13v e0_ hbra2
      wn idn2 pm3.26 e2 pm3.27 idn3 pm3.2an3 e223 in3 en3lp con3 e20 eleq2
      biimprcd e23 pm3.2 en2lp wsbc idn1 3simp3 e1_ 3simp2 ralcom2 3simp1 trel
      exp3a e121 e122 ra4sbc2 com13 sbid biimpi sbcoreleleq biimpd e12
      3ornot23 ex e222 in2 gen11nv dftr2 biimpri in1 ) CEZAFZBFZGZWDWCGZWCWDHIZ
      BCJZACJZDCGZKZDEZWKWEWDDGZLZWCDGZMZBNZANZWLWKWQAWIWIANMWKWKANMWHACOWIWBWJ
      APQWKWPBWIWIBNMWKWKBNMWGABCCRWIWBWJBPQWKWNWOWKWNDWCGZSZWCDHZSZWOWSXAIZWOW
      KWNWSWEWMWSKZMXDSWTWKWNWSXDWKWNWEWMWSWSXDWKWNWNWEWKWNTZWEWMUAUBZWKWNWNWMX
      EWEWMUCUBZWKWNWSUDWEWMWSUEUFUGWCWDDUHWSXDUIUJWKWNXAWEWFLZMXHSXBWKWNXAXHWK
      WNWEXAWFXHXFWKWNWMXAXAWFXGWKWNXAUDXAWFWMWCDWDUKULUMWEWFUNUMUGWCWDUOXAXHUI
      UJWKWJWNWGBDUPZXCWKWKWJWKUQZWBWIWJURUSZWKWNXIAWCUPZXIWKWGACJBCJZWNWCCGZWJ
      XLWKWIXMWKWKWIXJWBWIWJUTUSWGABCVAUSWKWBWNWEWDCGZXNWKWKWBXJWBWIWJVBUSZXFWK
      WBWNWMWJXOXPXGXKWBWMWJXOCWDDVCVDVEWBWEXOXNCWCWDVCVDVFXKWJXNXMXLWGBADCWCCV
      GVHVEXLXIXIAVIVJUBWJXIXCABDCVKVLVMWTXBXCWOMWSXAWOVNVOVPVQVRVRWLWRABDVSVTU
      SWA $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ 3ax5 . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |- (. A. x ( ph -> ( ps -> ch ) ) |-. A. x
       ( ph -> ( ps -> ch ) ) ). `
       <TR> <TD> 2:1,?: ~e1_   <TD> ` |- (. A. x ( ph -> ( ps -> ch ) ) |-. (
       A. x ph -> A. x ( ps -> ch ) ) ). `
       <TR> <TD> 3::         <TD> ` |- ( A. x ( ps -> ch ) -> ( A. x ps -> A. x
       ch ) ) `
       <TR> <TD> 4:2,3,?: ~e10 <TD> ` |- (. A. x ( ph -> ( ps -> ch ) ) |-. (
       A. x ph -> ( A. x ps -> A. x ch ) ) ). `
       <TR> <TD> qed:4:      <TD> ` |- ( A. x ( ph -> ( ps -> ch ) ) -> ( A. x
       ph -> ( A. x ps -> A. x ch ) ) ) `
       </TABLE> </HTML> $)
    3ax5VD $p |- ( A. x ( ph -> ( ps -> ch ) ) -> ( A. x ph ->
               ( A. x ps -> A. x ch ) ) ) $=
      ( wi wal idn1 ax-5 e1_ imim1 e10 in1 ) ABCEZEDFZADFZBDFCDFEZEZNOMDFZEZRPE
      QNNSNGAMDHIBCDHORPJKL $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ syl5imp . The following user's proof is
       completed by invoking mmj2's unify command and using mmj2's StepSelector
       to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |- (. ( ph -> ( ps -> ch ) ) |-. ( ph -> (
       ps -> ch ) ) ). `
       <TR> <TD> 2:1,?: ~e1_   <TD> ` |- (. ( ph -> ( ps -> ch ) ) |-. ( ps ->
       ( ph -> ch ) ) ). `
       <TR> <TD> 3::         <TD> ` |- (. ( ph -> ( ps -> ch ) ) , ( th -> ps )
       |-. ( th -> ps ) ). `
       <TR> <TD> 4:3,2,?: ~e21 <TD> ` |- (. ( ph -> ( ps -> ch ) ) , ( th -> ps
       )  |-. ( th -> ( ph -> ch ) ) ). `
       <TR> <TD> 5:4,?: ~e2    <TD> ` |- (. ( ph -> ( ps -> ch ) ) , ( th -> ps
       )  |-. ( ph -> ( th -> ch ) ) ). `
       <TR> <TD> 6:5:        <TD> ` |- (. ( ph -> ( ps -> ch ) ) |-. ( ( th ->
       ps ) -> ( ph -> ( th -> ch ) ) ) ). `
       <TR> <TD> qed:6:      <TD> ` |- ( ( ph -> ( ps -> ch ) ) -> ( ( th -> ps
       ) -> ( ph -> ( th -> ch ) ) ) ) `
       </TABLE> </HTML> $)
    syl5impVD $p |- ( ( ph -> ( ps -> ch ) ) -> ( ( th -> ps ) ->
                  ( ph -> ( th -> ch ) ) ) ) $=
      ( wi idn2 idn1 pm2.04 e1_ imim1 e21 e2 in2 in1 ) ABCEEZDBEZADCEEZEOPQOPDA
      CEZEZQOPPBREZSOPFOOTOGABCHIDBRJKDACHLMN $.
      $( [31-Dec-2011] $)
  $}

  ${
    idiVD.1 $e |- ph $.
    $( Virtual deduction proof of ~ idi . The following user's
       proof is completed by invoking mmj2's unify command and using mmj2's
       StepSelector to pick all remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> h1::        <TD> ` |- ph `
       <TR> <TD> qed:1,?: ~ e0_ <TD> ` |- ph `
       </TABLE> </HTML> $)
    idiVD $p |- ph $=
      ( id e0_ ) AABACD $.
      $( [31-Dec-2011] $)
  $}

  ${
    $( Closed form of ~ ancoms . The following user's proof is completed by
       invoking mmj2's unify command and using mmj2's StepSelector to pick all
       remaining steps of the Metamath proof.
       <HTML> <TABLE>
       <TR> <TD> 1::         <TD> ` |-  ( ( ph /\ ps ) <-> ( ps /\ ph ) )  `
       <TR> <TD> qed:1,?: ~e0_ <TD> ` |- ( ( ( ph /\ ps ) -> ch ) <-> ( ( ps /\
       ph ) -> ch ) ) `
       </TABLE> </HTML> $)
    ancomsimpVD $p |- ( ( ( ph /\ ps ) -> ch ) <-> ( ( ps /\ ph ) -> ch ) ) $=
      ( wa wb wi ancom imbi1 e0_ ) ABDZBADZEJCFKCFEABGJKCHI $.
      $( [25-Dec-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d C y $.  $d D x $.  $d D y $.
    $( Quantification restricted to a subclass for two quantifiers.  ~ ssralv
       for two quantifiers.  The following User's Proof is a Virtual Deduction
       proof completed automatically by the tools program
       completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm
       Megill's Metamath Proof Assistant. ~ ssralv2 is ~ ssralv2VD without
       virtual deductions and was automatically derived from ~ ssralv2VD .
       <HTML> <TABLE>
       <TR> <TD> 1::          <TD> ` |- (. ( A C_ B /\ C C_ D ) |-. ( A C_ B /\
       C C_ D ) ). `
       <TR> <TD> 2::          <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. A. x e. B A. y e. D ph ). `
       <TR> <TD> 3:1:         <TD> ` |- (. ( A C_ B /\ C C_ D ) |-. A C_ B ). `
       <TR> <TD> 4:3,2:       <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. A. x e. A A. y e. D ph ). `
       <TR> <TD> 5:4:         <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. A. x ( x e. A -> A. y e. D ph ) ). `
       <TR> <TD> 6:5:         <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. ( x e. A -> A. y e. D ph ) ). `
       <TR> <TD> 7::          <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph , x e. A |-. x e. A ). `
       <TR> <TD> 8:7,6:       <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph , x e. A |-. A. y e. D ph ). `
       <TR> <TD> 9:1:         <TD> ` |- (. ( A C_ B /\ C C_ D ) |-. C C_ D ). `
       <TR> <TD> 10:9,8:      <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph , x e. A |-. A. y e. C ph ). `
       <TR> <TD> 11:10:       <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. ( x e. A -> A. y e. C ph ) ). `
       <TR> <TD> 12::         <TD> ` |- ( ( A C_ B /\ C C_ D ) -> A. x ( A C_ B
       /\ C C_ D ) ) `
       <TR> <TD> 13::         <TD> ` |- ( A. x e. B A. y e. D ph -> A. x A. x
       e. B A. y e. D ph ) `
       <TR> <TD> 14:12,13,11: <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. A. x ( x e. A -> A. y e. C ph ) ). `
       <TR> <TD> 15:14:       <TD> ` |- (. ( A C_ B /\ C C_ D ) , A. x e. B A.
       y e. D ph |-. A. x e. A A. y e. C ph ). `
       <TR> <TD> 16:15:       <TD> ` |- (. ( A C_ B /\ C C_ D ) |-. ( A. x e. B
       A. y e. D ph -> A. x e. A A. y e. C ph ) ). `
       <TR> <TD> qed:16:      <TD> ` |- ( ( A C_ B /\ C C_ D ) -> ( A. x e. B
       A. y e. D ph -> A. x e. A A. y e. C ph ) ) `
       </TABLE> </HTML> $)
    ssralv2VD $p |- ( ( A C_ B /\ C C_ D ) -> ( A. x e. B A. y e. D ph ->
                    A. x e. A A. y e. C ph ) ) $=
      ( wss wa wral wi cv wcel wal ax-17 hbra1 idn1 pm3.27 e1_ idn3 pm3.26
      idn2 ssralv e12 df-ral biimpi e2 ax4 pm2.27 e32 e13 in3 gen21nv biimpri
      in2 in1 ) DEHZFGHZIZACGJZBEJZACFJZBDJZKUSVAVCUSVABLDMZVBKZBNZVCUSVAVEBUSB
      OUTBEPUSVAVDVBUSURVAVDUTVBUSUSURUSQZUQURRSUSVAVDVDVDUTKZUTUSVAVDTUSVAVHBN
      ZVHUSVAUTBDJZVIUSUQVAVAVJUSUSUQVGUQURUASUSVAUBUTBDEUCUDVJVIUTBDUEUFUGVHBU
      HUGVDUTUIUJACFGUCUKULUMVCVFVBBDUEUNUGUOUP $.
      $( [10-Feb-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( An element of an ordinal class is ordinal.  Proposition 7.6 of
       [TakeutiZaring] p. 36.  This is an alternate proof of ~ ordelord using
       the Axiom of Regularity indirectly through ~ dford2 .  dford2 is a
       weaker definition of ordinal number.  Given the Axiom of Regularity, it
       need not be assumed that ` _E Fr A ` because this is inferred by the
       Axiom of Regularity.  The following User's Proof is a Virtual Deduction
       proof completed automatically by the tools program
       completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm
       Megill's Metamath Proof Assistant. ~ ordelordaxr is ~ ordelordaxrVD
       without virtual deductions and was automatically derived from
       ~ ordelordaxrVD using the tools program
       translate..without..overwriting.cmd and Metamath's minimize command.
       <HTML> <TABLE>
       <TR> <TD> 1::        <TD> ` |- (. ( Ord A /\ B e. A ) |-. ( Ord A /\ B
       e. A ) ). `
       <TR> <TD> 2:1:       <TD> ` |- (. ( Ord A /\ B e. A ) |-. Ord A ). `
       <TR> <TD> 3:1:       <TD> ` |- (. ( Ord A /\ B e. A ) |-. B e. A ). `
       <TR> <TD> 4:2:       <TD> ` |- (. ( Ord A /\ B e. A ) |-. Tr A ). `
       <TR> <TD> 5:2:       <TD> ` |- (. ( Ord A /\ B e. A ) |-. A. x e. A A. y
       e. A ( x e. y \/ x = y \/ y e. x ) ). `
       <TR> <TD> 6:4,3:     <TD> ` |- (. ( Ord A /\ B e. A ) |-. B C_ A ). `
       <TR> <TD> 7:6,6,5:   <TD> ` |- (. ( Ord A /\ B e. A ) |-. A. x e. B A. y
       e. B ( x e. y \/ x = y \/ y e. x ) ). `
       <TR> <TD> 8::        <TD> ` |- ( ( x e. y \/ x = y \/ y e. x ) <-> ( x
       e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 9:8:       <TD> ` |- A. y ( ( x e. y \/ x = y \/ y e. x ) <->
       ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 10:9:      <TD> ` |- A. y e. A ( ( x e. y \/ x = y \/ y e. x )
       <-> ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 11:10:     <TD> ` |- ( A. y e. A ( x e. y \/ x = y \/ y e. x )
       <-> A. y e. A ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 12:11:     <TD> ` |- A. x ( A. y e. A ( x e. y \/ x = y \/ y
       e. x ) <-> A. y e. A ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 13:12:     <TD> ` |- A. x e. A ( A. y e. A ( x e. y \/ x = y
       \/ y e. x ) <-> A. y e. A ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 14:13:     <TD> ` |- ( A. x e. A A. y e. A ( x e. y \/ x = y
       \/ y e. x ) <-> A. x e. A A. y e. A ( x e. y \/ y e. x \/ x = y ) ) `
       <TR> <TD> 15:14,5:   <TD> ` |- (. ( Ord A /\ B e. A ) |-. A. x e. A A. y
       e. A ( x e. y \/ y e. x \/ x = y ) ). `
       <TR> <TD> 16:4,15,3: <TD> ` |- (. ( Ord A /\ B e. A ) |-. Tr B ). `
       <TR> <TD> 17:16,7:   <TD> ` |- (. ( Ord A /\ B e. A ) |-. Ord B ). `
       <TR> <TD> qed:17:    <TD> ` |- ( ( Ord A /\ B e. A ) -> Ord B ) `
       </TABLE> </HTML> $)
    ordelordaxrVD $p |- ( ( Ord A /\ B e. A ) -> Ord B ) $=
      ( vx vy word wcel wa wtr cv wceq w3o wral idn1 pm3.26 e1_ ordtr dford2
      pm3.27bi wb wal 3orcomb ax-gen alral e0_ ralbi e1bi pm3.27 tratrb 3exp
      e111 wss trss e11 wi ssralv2 ex pm3.26bi2 in1 ) AEZBAFZGZBEZVABHZCIZDIZFZ
      VDVEJZVEVDFZKZDBLCBLZVBVAAHZVFVHVGKZDALZCALZUTVCVAUSVKVAVAUSVAMZUSUTNOZAP
      OZVAVIDALZCALZVNVAUSVSVPUSVKVSCDAQROZVRVMSZCALZVSVNSWACTWBWACVIVLSZDALZWA
      WCDTWDWCDVFVGVHUAUBWCDAUCUDVIVLDAUEUDUBWACAUCUDVRVMCAUEUDUFVAVAUTVOUSUTUG
      OZVKVNUTVCCDABUHUIUJVABAUKZWFVSVJVAVKUTWFVQWEABULUMZWGVTWFWFVSVJUNVICDBAB
      AUOUPUJVBVCVJCDBQUQUMUR $.
      $( [12-Feb-2012] $)
  $}

  ${
    $( If a class equals the union of two other classes, then it equals the
       union of those two classes commuted. The following User's Proof is a
       Virtual Deduction proof completed automatically by the tools program
       completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm
       Megill's Metamath Proof Assistant. ~ equncom is ~ equncomVD without
       virtual deductions and was automatically derived from ~ equncomVD .
       <HTML> <TABLE>
       <TR> <TD> 1::    <TD> ` |- (. A = ( B u. C ) |-. A = ( B u. C ) ). `
       <TR> <TD> 2::    <TD> ` |- ( B u. C ) = ( C u. B ) `
       <TR> <TD> 3:1,2: <TD> ` |- (. A = ( B u. C ) |-. A = ( C u. B ) ). `
       <TR> <TD> 4:3:   <TD> ` |- ( A = ( B u. C ) -> A = ( C u. B ) ) `
       <TR> <TD> 5::    <TD> ` |- (. A = ( C u. B ) |-. A = ( C u. B ) ). `
       <TR> <TD> 6:5,2: <TD> ` |- (. A = ( C u. B ) |-. A = ( B u. C ) ). `
       <TR> <TD> 7:6:   <TD> ` |- ( A = ( C u. B ) -> A = ( B u. C ) ) `
       <TR> <TD> 8:4,7: <TD> ` |- ( A = ( B u. C ) <-> A = ( C u. B ) ) `
       </TABLE> </HTML> $)
    equncomVD $p |- ( A = ( B u. C ) <-> A = ( C u. B ) ) $=
      ( cun wceq idn1 uncom eqeq1 biimprd e10 in1 eqeq2 biimprcd impbii ) ABCDZ
      EZACBDZEZPRPPOQEZRPFBCGZPRSAOQHIJKRPRRSPRFTSPROQALMJKN $.
      $( [17-Feb-2012] $)
  $}

  ${
    equncomiVD.1 $e |- A = ( B u. C ) $.
    $( Inference form of ~ equncom . The following User's Proof is a
       Virtual Deduction proof completed automatically by the tools program
       completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm
       Megill's Metamath Proof Assistant. ~ equncomi is ~ equncomiVD without
       virtual deductions and was automatically derived from ~ equncomiVD .
       <HTML> <TABLE>
       <TR> <TD> h1::   <TD> ` |- A = ( B u. C ) `
       <TR> <TD> qed:1: <TD> ` |- A = ( C u. B ) `
       </TABLE> </HTML> $)
    equncomiVD $p |- A = ( C u. B ) $=
      ( cun wceq equncom biimpi e0_ ) ABCEFZACBEFZDJKABCGHI $.
      $( [18-Feb-2012] $)
  $}

  ${
    sucidALTVD.1 $e |- A e. _V $.
    $( A set belongs to its successor.  Alternate proof of ~ sucid .
       The following User's Proof is a Virtual Deduction proof
       completed automatically by the tools program
       completeusersproof.cmd, which invokes Mel O'Cat's mmj2 and Norm
       Megill's Metamath Proof Assistant.  ~ sucidALT is ~ sucidALTVD
       without virtual deductions and was automatically derived from
       ~ sucidALTVD . This proof illustrates that
       completeusersproof.cmd will generate a Metamath proof from any
       User's Proof which is "conventional" in the sense that no step
       is a virtual deduction, provided that all necessary unification
       theorems and transformation deductions are in set.mm.
       completeusersproof.cmd automatically converts such a
       conventional proof into a Virtual Deduction proof for which each
       step happens to be a 0-virtual hypothesis virtual deduction.
       The user does not need to search for reference theorem labels or
       deduction labels nor does he(she) need to use theorems and
       deductions which unify with reference theorems and deductions in
       set.mm.  All that is necessary is that each theorem or deduction
       of the User's Proof unifies with some reference theorem or
       deduction in set.mm or is a semantic variation of some theorem
       or deduction which unifies with some reference theorem or
       deduction in set.mm.  The definition of "semantic variation" has
       not been precisely defined.  If it is obvious that a theorem or
       deduction has the same meaning as another theorem or deduction,
       then it is a semantic variation of the latter theorem or
       deduction.  For example, step 4 of the User's Proof is a
       semantic variation of the definition (axiom)
       ` suc A = ( A u. { A } ) ` , which unifies with ~ df-suc , a
       reference definition (axiom) in set.mm.  Also, a theorem or
       deduction is said to be a semantic variation of a another
       theorem or deduction if it is obvious upon cursory inspection
       that it has the same meaning as a weaker form of the latter
       theorem or deduction.  For example, the deduction ` Ord A `
       infers  ` A. x e. A A. y e. A ( x e. y \/ x = y \/ y e. x ) ` is a
       semantic variation of the theorem ` ( Ord A <-> ( Tr A /\ A. x e. A `
       ` A. y e. A ( x e. y \/ x = y \/ y e. x ) ) ) ` , which unifies with
       the set.mm reference definition (axiom) ~ dford2 .
       <HTML> <TABLE>
       <TR> <TD> h1::     <TD> ` |- A e. _V   `
       <TR> <TD> 2:1:     <TD> ` |- A e. { A } `
       <TR> <TD> 3:2:     <TD> ` |- A e. ( { A } u. A ) `
       <TR> <TD> 4::      <TD> ` |- suc A = ( { A } u. A ) `
       <TR> <TD> qed:3,4: <TD> ` |- A e. suc A  `
       </TABLE> </HTML> $)
    sucidALTVD $p |- A e. suc A $=
      ( csn cun csuc wcel snid elun1 e0_ df-suc equncomi eleqtrri ) AACZADZAEZA
      MFANFABGAMAHIOAMAJKL $.
      $( [18-Feb-2012] $)
  $}

  ${
    sucidALT.1 $e |- A e. _V $.
    $( A set belongs to its successor.  This proof was automatically derived
       from ~ sucidALTVD using translate_without_overwriting.cmd and
       minimizing. $)
    sucidALT $p |- A e. suc A $=
      ( csn cun csuc wcel snid elun1 ax-mp df-suc equncomi eleqtrri ) AACZADZAE
      ZAMFANFABGAMAHIOAMAJKL $.
      $( [18-Feb-2012] $)
  $}

  ${
    sucidVD.1 $e |- A e. _V $.
    $( A set belongs to its successor.  The following User's Proof is a
       Virtual Deduction proof completed automatically by the tools
       program completeusersproof.cmd, which invokes Mel O'Cat's mmj2
       and Norm Megill's Metamath Proof Assistant.  ~ sucid is
       ~ sucidVD without virtual deductions and was automatically
       derived from ~ sucidVD .
       <HTML> <TABLE>
       <TR> <TD> h1::     <TD> ` |- A e. _V   `
       <TR> <TD> 2:1:     <TD> ` |- A e. { A } `
       <TR> <TD> 3:2:     <TD> ` |- A e. ( A u. { A } ) `
       <TR> <TD> 4::      <TD> ` |- suc A = ( A u. { A } ) `
       <TR> <TD> qed:3,4: <TD> ` |- A e. suc A  `
       </TABLE> </HTML> $)
    sucidVD $p |- A e. suc A $=
      ( csn cun csuc wcel snid elun2 e0_ df-suc eleqtrri ) AAACZDZAEALFAMFABGAL
      AHIAJK $.
      $( [18-Feb-2012] $)
  $}

$( (End of Alan Sare's mathbox.) $)


