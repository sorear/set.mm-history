$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Additional set theory definitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Undef $.
  $c fcard $.
  $c ser $.
  $c idsumf $.
  $c idsum $.
  $c Struct $.

  $( Extend class notation with undefined value function. $)
  cund $a class Undef $.

  $( Extend class notation with series generator. $)
  cser $a class ser $.

  $( Extend class notation with finite cardinality (member of ` NN0 ` ). $)
  cfc $a class fcard $.

  $( Extend class notation with finite idempotent sum. $)
  cidsumf $a class idsumf $.

  $( Extend class notation with idempotent sum. $)
  cidsum $a class idsum $.

  $( Extend class notation with extensible structure builders. $)
  cstru $a class Struct ( N , f , ph ) $.

  $( Define the undefined value function, whose value at set ` s ` is
     guaranteed not to be a member of ` s ` (see ~ pwuninel ).  Def. 8.3.36
     p. 181. $)
  df-undef $a |- Undef = ( s e. _V |-> ~P U. s ) $.

  ${
    $d f n p s t u x y $.
    $( Define finite cardinality.  Def. 8.3.43, p. 184. $)
    df-fcard $a |- fcard = ( x e. _V |-> if ( x e. Fin , ( iota n ( n e. NN
          /\ E. f f : x -1-1-onto-> ( 1 ... n ) ) ) , ( Undef ` NN0 ) ) ) $.

    $( Define series generator.  Def. 8.3.72, p. 198. $)
    df-ser $a |- ser = ( p e. _V , f e. _V |-> ran ( rec ( ( x e. _V , y e. _V
     |-> <. ( x + 1 ) , ( y p ( f ` ( x + 1 ) ) ) >. ) ,
             <. 1 , ( f ` 1 ) >. ) |` om ) ) $.

    $( Define finite idempotent sum.  Def. 8.3.74, p. 199. $)
    df-idsumf $a |- idsumf = ( p e. _V , s e. _V |->
    ( iota t A. f ( f : ( 1 ... ( fcard ` s ) ) -1-1-onto-> s
         -> t = ( ( p ser f ) ` ( fcard ` s ) ) ) ) ) $.

    $( Define idempotent sum.  Def. 8.3.75, p. 200. $)
    df-idsum $a |- idsum = ( p e. _V , s e. _V |->
        U. { u | E. t ( u = ( p idsumf t ) /\ t C_ s /\ t e. Fin ) } ) $.
  $}

  ${
    $d m N $.  $d m f $.  $d m ph $.
    $( Define an extensible structure builder.  This device provides the
       ability to succesively specialize structures.  For example, a ring can
       be a specialization of a group, and all theorems for groups will
       automatically be applicable to rings.  In the intended applications,
       ` N ` is a positive integer and and ` ph ` is a wff containing ` f `
       free in the form of occurrences of ` ( f `` 1 ) ` ,...,
       ` ( f `` N ) ` .  Note that ` f ` is a bound variable in
       ` Struct ( N , f , ph ) ` .  Def. 8.3.76 p. 201. $)
    df-struct $a |- Struct ( N , f , ph ) = { f | ( E. m e. NN ( N <_ m /\
          f Fn ( 1 ... m ) ) /\ ph ) } $.

  $}

  ${
    $d m f N $.  $d m ph $.  $d m ps $.
    $( The specialization of an extensible structure builder generates a
       subclass.  Th. 8.3.77 p. 201. $)
    strp1 $p |- ( N e. NN -> Struct ( ( N + 1 ) , f , ( ph /\ ps ) )
                    C_ Struct ( N , f , ph ) ) $=
      ( vm cn wcel c1 caddc co cv cle wbr cfz wfn wa wrex cab cstru cr wi w3a
      lep1 3ad2ant1 letr mpand 3expa nnre peano2re ancli syl syl2an anim1d
      r19.22dva pm3.26 a1i anim12d ss2abdv df-struct 3sstr4g ) DFGZDHIJZEKZLMZC
      KHVCNJOZPZEFQZABPZPZCRDVCLMZVEPZEFQZAPZCRVHCVBSACDSVAVIVMCVAVGVLVHAVAVFVK
      EFVAVCFGZPVDVJVEDTGZVBTGZPZVCTGZVDVJUAZVAVNVOVPVRVSVOVPVRUBDVBLMZVDVJVOVP
      VTVRDUCUDDVBVCUEUFUGVAVOVQDUHVOVPDUIUJUKVCUHULUMUNVHAUAVAABUOUPUQURVHCEVB
      USACEDUSUT $.
      $( [?] $) $( [29-Jul-2011] $)
  $}

  ${
    $d f m F $.  $d f m N $.  $d f ps $.  $d m ph $.
    elstr.1 $e |- ( f = F -> ( ph <-> ps ) ) $.
    $( Membership in an extensible structure builder. $)
    elstr $p |- ( F e. Struct ( N , f , ph ) <->
                      ( E. m e. NN ( N <_ m /\ F Fn ( 1 ... m ) ) /\ ps ) ) $=
      ( cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex cab df-struct eleq2i
      cvv oprex fnex mpan2 ad2antll r19.23aiva adantr wceq fneq1 anbi2d
      rexbidv anbi12d elab3 bitri ) EACFHZIEFDJZKLZCJZMURNOZPZQZDRSZAQZCTZIUSEV
      APZQZDRSZBQZUQVFEACDFUAUBVEVJCEVIEUCIZBVHVKDRVGVKURRIUSVGVAUCIVKMURNUDVAU
      CEUEUFUGUHUIUTEUJZVDVIABVLVCVHDRVLVBVGUSVAUTEUKULUMGUNUOUP $.
      $( [?] $) $( [12-Aug-2011] $)

    $( Implication of membership in an extensible structure builder. $)
    elstri $p |- ( F e. Struct ( N , f , ph ) -> ps ) $=
      ( vm cstru wcel cv cle wbr c1 cfz co wfn wa cn wrex elstr pm3.27bi ) DACE
      HIEGJZKLDMUBNOPQGRSBABCGDEFTUA $.
      $( [?] $) $( [12-Aug-2011] $)
  $}

