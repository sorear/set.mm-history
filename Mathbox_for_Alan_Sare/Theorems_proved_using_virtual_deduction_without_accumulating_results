$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Theorems proved using virtual deduction without accumulating results
    and their shortened predicate calculus counterparts
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( Virtual deduction proof of the left-to-right implication of ~ dftr4 .  A
       transitive class is a subset of its power class.  This proof corresponds
       to the virtual deduction proof of ~ trsspw without accumulating
       results. $)
    trsspwALT $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 idn1 idn2 trss e12 visset elpw
      e2bir in2 gen11 bi2 e01 in1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUFBAUEKUDU
      JBUDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNOUGABPQRSTUFUKUAUBUC $.
      $( [29-Apr-2011] $)
  $}

  ${
    $d x A $.
    $( Virtual deduction proof of ~ trsspwALT .  This proof is the same as the
       proof of ~ trsspwALT except each meta-connective symbol is replaced by
       its non-meta-connective symbol equivalent.  A transitive class is a
       subset of its power class. $)
    trsspwALT2 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 id idd trss ee12 visset elpw
      syl6ibr idi2 19.21aiv bi2 ee01 idi1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUF
      BAUEKUDUJBUDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNOUGABPQRSTUFUKUAUBUC $.
      $( [29-Apr-2011] $)
  $}

  ${
    $d x A $.
    $( Short predicate calculus proof of the left-to-right implication of
       ~ dftr4 .  A transitive class is a subset of its power class.  This
       proof was constructed by applying Metamath's minimize command to the
       proof of ~ trsspwALT2 , which is the virtual deduction proof ~ trsspwALT
       without meta-connectives. $)
    trsspwALT3 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw cv wcel wss trss visset elpw syl6ibr ssrdv ) ACZBAADZMBEZAFO
      AGONFAOHOABIJKL $.
      $( [30-Apr-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of the right-to-left implication of ~ dftr4 .  A
       class which is a subclass of its power class is transitive.  This proof
       corresponds to the virtual deduction proof of ~ sspwtr without
       accumulating results. $)
    sspwtrALT $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr cv wcel wa wi wal wb dftr2 idn1 idn2 pm3.27 e2 ssel
      e12 elpwi pm3.26 e22 in2 gen12 bi2 e01 in1 ) AADZEZAFZUJBGZCGZHZULAHZIZUK
      AHZJZCKBKZLUIURUJBCAMUIUQBCUIUOUPUIUOULAEZUMUPUIUOULUHHZUSUIUIUOUNUTUINUI
      UOUOUNUIUOOZUMUNPQAUHULRSULATQUIUOUOUMVAUMUNUAQULAUKRUBUCUDUJURUEUFUG $.
      $( [2-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ sspwtrALT .  This proof is the same as the
       proof of ~ sspwtrALT except each meta-connective symbol is replaced by
       its non-meta-connective symbol equivalent.  A class which is a subclass
       of its power class is transitive. $)
    sspwtrALT2 $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr wi cv wcel wa wal wb dftr2 id idd pm3.27 syl6 ssel
      ee12 elpwi pm3.26 ee22 idi 19.21aivv bi2 ee01 ) AADZEZAFZGUIBHZCHZIZUKAIZ
      JZUJAIZGZCKBKZLUHUQUIBCAMUHUPBCUHUPGUHUNUKAEZULUOUHUNUKUGIZURUHUHUNUMUSUH
      NUHUNUNUMUHUNOZULUMPQAUGUKRSUKATQUHUNUNULUTULUMUAQUKAUJRUBUCUDUIUQUEUFUC
      $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Short predicate calculus proof of the right-to-left implication of
       ~ dftr4 .  A class which is a subclass of its power class is
       transitive.  This proof was constructed by applying Metamath's minimize
       command to the proof of ~ sspwtrALT2 , which is the virtual deduction
       proof ~ sspwtrALT without meta-connectives. $)
    sspwtrALT3 $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss cv wcel wa wi wal wtr ssel adantld elpwi syl6 pm3.26 a1i
      ee22 19.21aivv dftr2 sylibr ) AADZEZBFZCFZGZUEAGZHZUDAGZIZCJBJAKUCUJBCUCU
      HUEAEZUFUIUCUHUEUBGZUKUCUGULUFAUBUELMUEANOUHUFIUCUFUGPQUEAUDLRSBCATUA $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( The power class of a transitive class is transitive.  Virtual deduction
       proof without accumulating results. $)
    pwtrALT $p |- ( Tr A -> Tr ~P A ) $=
      ( vz vy wtr cpw cv wcel wa wi wal wb dftr2 wss idn1 idn2 pm3.27 e2 elpwi
      pm3.26 ssel e22 trss e12 visset elpw e2bir in2 gen12 bi2 e01 in1 ) ADZAEZ
      DZUNBFZCFZGZUPUMGZHZUOUMGZIZCJBJZKULVBUNBCUMLULVABCULUSUTULUSUOAMZUTULULU
      SUOAGZVCULNULUSUPAMZUQVDULUSURVEULUSUSURULUSOZUQURPQUPARQULUSUSUQVFUQURSQ
      UPAUOTUAAUOUBUCUOABUDUEUFUGUHUNVBUIUJUK $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    pwtrrALT.1 $e |- A e. _V $.
    $( A set is transitive if its power set is.  Virtual deduction proof
       without accumulating results. $)
    pwtrrALT $p |- ( Tr ~P A -> Tr A ) $=
      ( vz vy cpw wtr cv wcel wa wi wal wb dftr2 wss idn1 idn2 pm3.27 e2 pwid
      trel exp3a e120 elpwi pm3.26 ssel e22 in2 gen12 bi2 e01 in1 ) AEZFZAFZUNC
      GZDGZHZUPAHZIZUOAHZJZDKCKZLUMVBUNCDAMUMVACDUMUSUTUMUSUPANZUQUTUMUSUPULHZV
      CUMUMUSURAULHZVDUMOUMUSUSURUMUSPZUQURQRABSUMURVEVDULUPATUAUBUPAUCRUMUSUSU
      QVFUQURUDRUPAUOUEUFUGUHUNVBUIUJUK $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( If a class is an element of another class, then its singleton is a
       subclass of that other class.  This theorem is the left-to-right
       implication of the biconditional ~snss .  Unlike ~snss , ` A ` may be a
       proper class. $)
    snssr $p |- ( A e. B -> { A } C_ B ) $=
      ( vx wcel csn wss cv wi wal wb dfss2 wceq idn1 idn2 elsn e2bi eleq1a e12
      in2 gen11 bi2 e01 in1 ) ABDZAEZBFZUFCGZUEDZUGBDZHZCIZJUDUKUFCUEBKUDUJCUDU
      HUIUDUDUHUGALZUIUDMUDUHUHULUDUHNCAOPABUGQRSTUFUKUAUBUC $.
      $( [15-Jun-2011] $)
  $}

  ${
    $( If a class is contained in another class, then its singleton is
       contained in the power class of that other class.  This theorem is the
       left-to-right implication of the biconditional ~snelpw .  Unlike
       ~snelpw , ` A ` may be a proper class. $)
    snelpwr $p |- ( A e. B -> { A } e. ~P B ) $=
      ( wcel csn cpw cvv wss snex idn1 snssr e1_ elpwg biimprd e01 in1 ) ABCZAD
      ZBECZQFCZPQBGZRAHPPTPIABJKSRTQBFLMNO $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d A x y $.
    $( A class is a subclass of the union of its power class.  This theorem is
       the right-to-left subclass lemma of ~ unipw . $)
    unipwr $p |- A C_ U. ~P A $=
      ( vx cpw cuni cv wcel csn visset snid idn1 snelpwr e1_ elunii e01an in1
      ssriv ) BAACZDZBEZAFZSRFZSSGZFTUBQFZUASBHITTUCTJSAKLSUBQMNOP $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ sstr , transitivity of subclasses, Theorem
       6 of [Suppes] p. 23. $)
    sstrALT2 $p |- ( ( A C_ B /\ B C_ C ) -> A C_ C ) $=
      ( vx wss wa cv wcel wi wal wb dfss2 idn1 pm3.27 e1_ pm3.26 idn2 ssel2
      e12an in2 gen11 bi2 e01 in1 ) ABEZBCEZFZACEZUHDGZAHZUICHZIZDJZKUGUMUHDACL
      UGULDUGUJUKUGUFUJUIBHZUKUGUGUFUGMZUEUFNOUGUEUJUJUNUGUGUEUOUEUFPOUGUJQABUI
      RSBCUIRSTUAUHUMUBUCUD $.
      $( [15-Jun-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ suctr .  The sucessor of a transitive class
       is transitive. $)
    suctrALT2 $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr csuc cv wcel wa wi wal wb dftr2 wceq wo wss sssucid idn1
      idn2 pm3.26 e2 idn3 trel exp3a e123 ssel e03 in3 eleq2 biimpcd e23
      pm3.27 elsuci jao e222 in2 gen12 bi2 e01 in1 ) ADZAEZDZVBBFZCFZGZVDVAGZHZ
      VCVAGZIZCJBJZKUTVJVBBCVALUTVIBCUTVGVHUTVGVDAGZVHIVDAMZVHIVKVLNZVHUTVGVKVH
      AVAOZUTVGVKVCAGZVHAPZUTUTVGVEVKVKVOUTQUTVGVGVEUTVGRZVEVFSTZUTVGVKUAUTVEVK
      VOAVCVDUBUCUDAVAVCUEZUFUGUTVGVLVHVNUTVGVLVOVHVPUTVGVEVLVLVOVRUTVGVLUAVLVE
      VOVDAVCUHUIUJVSUFUGUTVGVFVMUTVGVGVFVQVEVFUKTVDAULTVKVHVLUMUNUOUPVBVJUQURU
      S $.
      $( [15-Jun-2011] $)
  $}

$( (End of Alan Sare's mathbox.) $)

