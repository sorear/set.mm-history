$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
    Theorems proved using virtual deduction without accumulating results
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( Virtual deduction proof of the left-to-right implication of ~ dftr4 .  A
       transitive class is a subset of its power class.  This proof corresponds
       to the virtual deduction proof of ~ dftr4 without accumulating
       results. $)
    trsspwALT $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 idn1 idn2 trss e12 visset elpw
      e2bir in2 gen11 bi2 e01 in1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUFBAUEKUDU
      JBUDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNOUGABPQRSTUFUKUAUBUC $.
      $( [29-Apr-2011] $)
  $}

  ${
    $d x A $.
    $( Virtual deduction proof of ~ trsspwALT .  This proof is the same as the
       proof of ~ trsspwALT except each meta-connective symbol is replaced by
       its non-meta-connective symbol equivalent.  A transitive class is a
       subset of its power class. $)
    trsspwALT2 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw wss cv wcel wi wal wb dfss2 id idd trss ee12 visset elpw
      syl6ibr iin2 19.21aiv bi2 ee01 iin1 ) ACZAADZEZUFBFZAGZUGUEGZHZBIZJUDUKUF
      BAUEKUDUJBUDUHUIUDUHUGAEZUIUDUDUHUHULUDLUDUHMAUGNOUGABPQRSTUFUKUAUBUC $.
      $( [23-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( Short predicate calculus proof of the left-to-right implication of
       ~ dftr4 .  A transitive class is a subset of its power class.  This
       proof was constructed by applying Metamath's minimize command to the
       proof of ~ trsspwALT2 , which is the virtual deduction proof ~ trsspwALT
       without meta-connectives. $)
    trsspwALT3 $p |- ( Tr A -> A C_ ~P A ) $=
      ( vx wtr cpw cv wcel wss trss visset elpw syl6ibr ssrdv ) ACZBAADZMBEZAFO
      AGONFAOHOABIJKL $.
      $( [30-Apr-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of the right-to-left implication of ~ dftr4 .  A
       class which is a subclass of its power class is transitive.  This proof
       corresponds to the virtual deduction proof of ~ sspwtr without
       accumulating results. $)
    sspwtrALT $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr cv wcel wa wi wal wb dftr2 idn1 idn2 pm3.27 e2 ssel
      e12 elpwi pm3.26 e22 in2 gen12 bi2 e01 in1 ) AADZEZAFZUJBGZCGZHZULAHZIZUK
      AHZJZCKBKZLUIURUJBCAMUIUQBCUIUOUPUIUOULAEZUMUPUIUOULUHHZUSUIUIUOUNUTUINUI
      UOUOUNUIUOOZUMUNPQAUHULRSULATQUIUOUOUMVAUMUNUAQULAUKRUBUCUDUJURUEUFUG $.
      $( [2-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ sspwtrALT .  This proof is the same as the
       proof of ~ sspwtrALT except each meta-connective symbol is replaced by
       its non-meta-connective symbol equivalent.  A class which is a subclass
       of its power class is transitive. $)
    sspwtrALT2 $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss wtr cv wcel wa wi wal wb dftr2 id idd pm3.27 syl6 ssel
      ee12 elpwi pm3.26 ee22 iin2 19.21aivv bi2 ee01 iin1 ) AADZEZAFZUJBGZCGZHZ
      ULAHZIZUKAHZJZCKBKZLUIURUJBCAMUIUQBCUIUOUPUIUOULAEZUMUPUIUOULUHHZUSUIUIUO
      UNUTUINUIUOUOUNUIUOOZUMUNPQAUHULRSULATQUIUOUOUMVAUMUNUAQULAUKRUBUCUDUJURU
      EUFUG $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Short predicate calculus proof of the right-to-left implication of
       ~ dftr4 .  A class which is a subclass of its power class is
       transitive.  This proof was constructed by applying Metamath's minimize
       command to the proof of ~ sspwtrALT2 , which is the virtual deduction
       proof ~ sspwtrALT without meta-connectives. $)
    sspwtrALT3 $p |- ( A C_ ~P A -> Tr A ) $=
      ( vz vy cpw wss cv wcel wa wi wal wtr ssel adantld elpwi syl6 pm3.26 a1i
      ee22 19.21aivv dftr2 sylibr ) AADZEZBFZCFZGZUEAGZHZUDAGZIZCJBJAKUCUJBCUCU
      HUEAEZUFUIUCUHUEUBGZUKUCUGULUFAUBUELMUEANOUHUFIUCUFUGPQUEAUDLRSBCATUA $.
      $( [3-May-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ pwtr . $)
    pwtrVD $p |- ( Tr A -> Tr ~P A ) $=
      ( vz vy wtr cpw cv wcel wa wi wal wb dftr2 wss idn1 idn2 pm3.27 e2 elpwi
      pm3.26 ssel e22 trss e12 visset elpw e2bir in2 gen12 bi2 e01 in1 ) ADZAEZ
      DZUNBFZCFZGZUPUMGZHZUOUMGZIZCJBJZKULVBUNBCUMLULVABCULUSUTULUSUOAMZUTULULU
      SUOAGZVCULNULUSUPAMZUQVDULUSURVEULUSUSURULUSOZUQURPQUPARQULUSUSUQVFUQURSQ
      UPAUOTUAAUOUBUCUOABUDUEUFUGUHUNVBUIUJUK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( The power class of a transitive class is transitive.  The proof of this
       theorem was automatically generated from ~ pwtrVD using a tools command
       file, translateMWO.cmd , by translating the proof into its
       non-meta-connective form and minimizing it. $)
    pwtr $p |- ( Tr A -> Tr ~P A ) $=
      ( vz vy wtr wel cv cpw wcel wa wi wal wss pm3.27 a1i elpwi syl6 pm3.26
      ssel ee22 trss syld visset elpw syl6ibr 19.21aivv dftr2 sylibr ) ADZBCEZC
      FZAGZHZIZBFZUKHZJZCKBKUKDUHUPBCUHUMUNALZUOUHUMUNAHZUQUHUMUJALZUIURUHUMULU
      SUMULJUHUIULMNUJAOPUMUIJUHUIULQNUJAUNRSAUNTUAUNABUBUCUDUEBCUKUFUG $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    pwtrrVD.1 $e |- A e. _V $.
    $( Virtual deduction proof of ~ pwtrr . $)
    pwtrrVD $p |- ( Tr ~P A -> Tr A ) $=
      ( vz vy cpw wtr cv wcel wa wi wal wb dftr2 wss idn1 idn2 pm3.27 e2 pwid
      trel exp3a e120 elpwi pm3.26 ssel e22 in2 gen12 bi2 e01 in1 ) AEZFZAFZUNC
      GZDGZHZUPAHZIZUOAHZJZDKCKZLUMVBUNCDAMUMVACDUMUSUTUMUSUPANZUQUTUMUSUPULHZV
      CUMUMUSURAULHZVDUMOUMUSUSURUMUSPZUQURQRABSUMURVEVDULUPATUAUBUPAUCRUMUSUSU
      QVFUQURUDRUPAUOUEUFUGUHUNVBUIUJUK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    pwtrr.1 $e |- A e. _V $.
    $( A set is transitive if its power set is.  The proof of this theorem was
       automatically generated from ~ pwtrrVD using a tools command file,
       translateMWO.cmd , by translating the proof into its non-meta-connective
       form and minimizing it. $)
    pwtrr $p |- ( Tr ~P A -> Tr A ) $=
      ( vz vy cpw wtr wel cv wcel wa wi wal wss id pm3.27 a1i pwid trel exp3a
      ee120 elpwi syl6 pm3.26 ssel ee22 19.21aivv dftr2 sylibr ) AEZFZCDGZDHZAI
      ZJZCHZAIZKZDLCLAFUJUQCDUJUNULAMZUKUPUJUNULUIIZURUJUJUNUMAUIIZUSUJNUNUMKUJ
      UKUMOPABQUJUMUTUSUIULARSTULAUAUBUNUKKUJUKUMUCPULAUOUDUEUFCDAUGUH $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Virtual deduction proof of ~ snssiALT . $)
    snssiALTVD $p |- ( A e. B -> { A } C_ B ) $=
      ( vx wcel csn wss cv wi wal wb dfss2 wceq idn1 idn2 elsn e2bi eleq1a e12
      in2 gen11 bi2 e01 in1 ) ABDZAEZBFZUFCGZUEDZUGBDZHZCIZJUDUKUFCUEBKUDUJCUDU
      HUIUDUDUHUGALZUIUDMUDUHUHULUDUHNCAOPABUGQRSTUFUKUAUBUC $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( If a class is an element of another class, then its singleton is a
       subclass of that other class.  Alternate proof of ~ snssi .  This
       theorem was automatically generated from ~ snssiALTVD using the command
       file translate_without_overwriting_minimize_excluding_du
       plicates.cmd . $)
    snssiALT $p |- ( A e. B -> { A } C_ B ) $=
      ( vx wcel cv csn wi wal wss wceq eleq1a elsn syl5ib 19.21aiv dfss2
      sylibr ) ABDZCEZAFZDZRBDZGZCHSBIQUBCQRAJUATABRKCALMNCSBOP $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    snsslVD.1 $e |- A e. _V $.
    $( Virtual deduction proof of ~ snssl . $)
    snsslVD $p |- ( { A } C_ B -> A e. B ) $=
      ( csn wss wcel idn1 snid ssel2 e10an in1 ) ADZBEZABFZMMALFNMGACHLBAIJK $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    snssl.1 $e |- A e. _V $.
    $( If a singleton is a subclass of another class, then the singleton's
       element is an element of that other class.  This theorem is the
       right-to-left implication of the biconditional ~ snss .  The proof of
       this theorem was automatically generated from ~ snsslVD using a tools
       command file, translateMWO.cmd , by translating the proof into its
       non-meta-connective form and minimizing it. $)
    snssl $p |- ( { A } C_ B -> A e. B ) $=
      ( csn wss wcel snid ssel2 mpan2 ) ADZBEAJFABFACGJBAHI $.
      $( [25-Aug-2011] $)
  $}

  ${
    $( Virtual deduction proof of ~ snelpwr . $)
    snelpwrVD $p |- ( A e. B -> { A } e. ~P B ) $=
      ( wcel csn cpw cvv wss snex idn1 snssi e1_ elpwg biimprd e01 in1 ) ABCZAD
      ZBECZQFCZPQBGZRAHPPTPIABJKSRTQBFLMNO $.
      $( [25-Aug-2011] $)
  $}

  ${
    $( If a class is contained in another class, then its singleton is
       contained in the power class of that other class.  This theorem is the
       left-to-right implication of the biconditional ~ snelpw .  Unlike
       ~ snelpw , ` A ` may be a proper class.  The proof of this theorem was
       automatically generated from ~ snelpwrVD using a tools command file,
       translateMWO.cmd , by translating the proof into its non-meta-connective
       form and minimizing it. $)
    snelpwr $p |- ( A e. B -> { A } e. ~P B ) $=
      ( csn cvv wcel wss cpw snex snssi elpwg biimprd ee01 ) ACZDEZABEMBFZMBGEZ
      AHABINPOMBDJKL $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x y $.
    $( Virtual deduction proof of ~ unipwr . $)
    unipwrVD $p |- A C_ U. ~P A $=
      ( vx cpw cuni cv wcel csn visset snid idn1 snelpwr e1_ elunii e01an in1
      ssriv ) BAACZDZBEZAFZSRFZSSGZFTUBQFZUASBHITTUCTJSAKLSUBQMNOP $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d A x y $.
    $( A class is a subclass of the union of its power class.  This theorem is
       the right-to-left subclass lemma of ~ unipw .  The proof of this theorem
       was automatically generated from ~ unipwrVD using a tools command file ,
       translateMWO.cmd , by translating the proof into its non-meta-connective
       form and minimizing it. $)
    unipwr $p |- A C_ U. ~P A $=
      ( vx cpw cuni cv csn wcel elunii visset snid snelpwr sylancr ssriv ) BAAC
      ZDZBEZPFZGQNGPOGPAGPQNHPBIJPAKLM $.
      $( [25-Aug-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ sstrALT2 . $)
    sstrALT2VD $p |- ( ( A C_ B /\ B C_ C ) -> A C_ C ) $=
      ( vx wss wa cv wcel wi wal wb dfss2 idn1 pm3.27 e1_ pm3.26 idn2 ssel2
      e12an in2 gen11 bi2 e01 in1 ) ABEZBCEZFZACEZUHDGZAHZUICHZIZDJZKUGUMUHDACL
      UGULDUGUJUKUGUFUJUIBHZUKUGUGUFUGMZUEUFNOUGUEUJUJUNUGUGUEUOUEUFPOUGUJQABUI
      RSBCUIRSTUAUHUMUBUCUD $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Virtual deduction proof of ~ sstr , transitivity of subclasses, Theorem
       6 of [Suppes] p. 23.  This theorem was automatically generated from
       ~ sstrALT2VD using the command file translate_without_overwriting.cmd .
       It was not minimized because the automated minimization excluding
       duplicates generates a minimized proof which, although not directly
       containing any duplicates, indirectly contains a duplicate.  That is,
       the trace back of the minimized proof contains a duplicate.  This is
       undesirable because some step(s) of the minimized proof use the proven
       theorem. $)
    sstrALT2 $p |- ( ( A C_ B /\ B C_ C ) -> A C_ C ) $=
      ( vx wss wa cv wcel wi wal wb dfss2 id pm3.27 syl pm3.26 idd ssel2
      ee12an iin2 19.21aiv bi2 ee01 iin1 ) ABEZBCEZFZACEZUHDGZAHZUICHZIZDJZKUGU
      MUHDACLUGULDUGUJUKUGUFUJUIBHZUKUGUGUFUGMZUEUFNOUGUEUJUJUNUGUGUEUOUEUFPOUG
      UJQABUIRSBCUIRSTUAUHUMUBUCUD $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ suctrALT2 . $)
    suctrALT2VD $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr csuc cv wcel wa wi wal wb dftr2 wceq wo wss sssucid idn1
      idn2 pm3.26 e2 idn3 trel exp3a e123 ssel e03 in3 eleq2 biimpcd e23
      pm3.27 elsuci jao e222 in2 gen12 bi2 e01 in1 ) ADZAEZDZVBBFZCFZGZVDVAGZHZ
      VCVAGZIZCJBJZKUTVJVBBCVALUTVIBCUTVGVHUTVGVDAGZVHIVDAMZVHIVKVLNZVHUTVGVKVH
      AVAOZUTVGVKVCAGZVHAPZUTUTVGVEVKVKVOUTQUTVGVGVEUTVGRZVEVFSTZUTVGVKUAUTVEVK
      VOAVCVDUBUCUDAVAVCUEZUFUGUTVGVLVHVNUTVGVLVOVHVPUTVGVEVLVLVOVRUTVGVLUAVLVE
      VOVDAVCUHUIUJVSUFUGUTVGVFVMUTVGVGVFVQVEVFUKTVDAULTVKVHVLUMUNUOUPVBVJUQURU
      S $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d z A $.  $d y A $.  $d z y $.
    $( Virtual deduction proof of ~ suctr .  The sucessor of a transitive class
       is transitive.  This proof was generated automatically from the virtual
       deduction proof ~ suctrALT2VD using the tools command file
       translate_without_overwriting_minimize_excluding_duplicates.cmd . $)
    suctrALT2 $p |- ( Tr A -> Tr suc A ) $=
      ( vz vy wtr wel cv csuc wcel wa wi wal wceq wo wss sssucid trel exp3a
      adantrd ssel ee03 pm3.26 a1i eleq2 biimpcd syl6 pm3.27 elsuci jao ee222
      19.21aivv dftr2 sylibr ) ADZBCEZCFZAGZHZIZBFZUPHZJZCKBKUPDUMVABCUMURUOAHZ
      UTJUOALZUTJVBVCMZUTAUPNZUMURVBUSAHZUTAOZUMUNVBVFJUQUMUNVBVFAUSUOPQRAUPUSS
      ZTVEUMURVCVFUTVGUMURUNVCVFJURUNJUMUNUQUAUBVCUNVFUOAUSUCUDUEVHTUMURUQVDURU
      QJUMUNUQUFUBUOAUGUEVBUTVCUHUIUJBCUPUKUL $.
      $( [11-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Virtual deduction proof of ~ elex2 . $)
    elex2VD $p |- ( A e. B -> E. x x e. B ) $=
      ( wcel cv wex wceq wi wal idn1 idn2 eleq1a e12 in2 gen11 elex e1_ exim
      e11 in1 ) BCDZAEZCDZAFZUAUBBGZUCHZAIUEAFZUDUAUFAUAUEUCUAUAUEUEUCUAJZUAUEK
      BCUBLMNOUAUAUGUHABCPQUEUCARST $.
      $( [25-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( If a class contains another class, then it contains some set.  This
       proof was generated automatically from the virtual deduction proof
       ~ elex2 using the tools command file
       translate_without_overwriting_minimize_excluding_duplicates.cmd . $)
    elex2 $p |- ( A e. B -> E. x x e. B ) $=
      ( cv wceq wcel wi wal wex exim eleq1a 19.21aiv elex sylc ) ADZBEZOCFZGZAH
      PAIQAIBCFZPQAJSRABCOKLABCMN $.
      $( [25-Sep-2011] $)
  $}

$( (End of Alan Sare's mathbox.) $)

