$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                  Mathbox for Alan Sare
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  ${
    snelpwi.1 $e |- A e. V $.
    snelpwi.2 $e |- A e. B $.
    $( Inference form of ~ snelpw . $)
    snelpwi $p |- { A } e. P~ B $=
      ( wcel csn cpw snelpw mpbi ) ABEAFBGEDABCHI $.
      $( [31-Dec-2008] $) $( [30-Dec-2008] $)
  $}

  ${
    $d x A $. $d x B $. $d x C $.
    eluniii.1 $e |- A e. B $.
    eluniii.2 $e |- B e. C $.
    $( Inference form of ~ elunii . $)
    eluniii $p |- A e. U. C $=
      ( wcel wa cuni pm3.2i elunii ax-mp ) ABFZBCFZGACHFLMDEIABCJK $.
      $( [31-Dec-2008] $) $( [30-Dec-2008] $)
  $}

  ${
    ssunipwALTlem1.1 $e |- x e. A $.
    $( An element of a class is an element of the union of that class's power
       class $)
    ssunipwALTlem1 $p |- x e. U. P~ A $=
      ( cv csn cpw visset snid snelpwi eluniii ) ADZKEBFKAGZHKBLCIJ $.
      $( [31-Dec-2008] $) $( [30-Dec-2008] $)
  $}

  ${
    $d x A $.
    $( Every set is a subset of the union of its power set.  ` x ` and ` A `
       must be distinct because this is a requirement of ~ ssriv . $)
    ssunipwALT $p |- A (_ U. P~ A $=
      ( vx cpw cuni cv wcel csn cif wceq wb pweq unieq eleq2 3syl visset snid
      elimhyp ssunipwALTlem1 dedth ssriv ) BAACZDZBEZAFZUCUBFZUCUDAUCGZHZCZDZFZ
      AUFAUGIUAUHIUBUIIUEUJJAUGKUAUHLUBUIUCMNBUGUDUCUGFUCUFFAUFAUGUCMUFUGUCMUCB
      OPQRST $.
      $( [31-Dec-2008] $) $( [30-Dec-2008] $)
  $}

  ${
    elsucii.1 $e |- A e. suc B $.
    $( Inference form of elsuci. $)
    elsucii $p |- ( A e. B \/ A = B ) $=
      ( csuc wcel wceq wo elsuci ax-mp ) ABDEABEABFGCABHI $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    jaoii.1 $e |- ( ph -> ps ) $.
    jaoii.2 $e |- ( ch -> ps ) $.
    jaoii.3 $e |- ( ph \/ ch ) $.
    $( Inference form of jaoi.. $)
    jaoii $p |- ps $=
      ( wo jaoi ax-mp ) ACGBFABCDEHI $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    treli.1 $e |- Tr A $.
    treli.2 $e |- B e. C $.
    treli.3 $e |- C e. A $.
    $( Inference form of trel. $)
    treli $p |- B e. A $=
      ( wcel wa pm3.2i wtr wi trel ax-mp ) BCGZCAGZHZBAGZNOEFIAJPQKDABCLMM $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    dt.1 $e |- ph $.
    dt.2 $e |- ps $.
    $( Pseudo Deduction Theorem. $)
    dt $p |- ( ph -> ps ) $=
      ( a1i ) BADE $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    $d y A $. $d x A $. $d x y $.
    dftr2i.1 $e |- A. x A. y ( ( x e. y /\ y e. A ) -> x e. A ) $.
    $( Inference form of dftr2. $)
    dftr2i $p |- Tr A  $=
      ( wtr cv wcel wa wi wal dftr2 mpbir ) CEAFZBFZGNCGHMCGIBJAJDABCKL $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    $d z A $. $d y A $. $d z y $.
    suctr.1 $e |- Tr A $.
    suctr.2 $e |- ( z e. y /\ y e. suc A ) $.
    suctr.3 $e |- y e. A $.
    suctr.4 $e |- y = A $.
    $( The successor of a transitive class is transitive.  Below is the
       original proof in the actual sequence constructed.  Compare the direct
       proof, ~ suctr .
       <PRE>
        Permitted Assumptions    Hyp       Ref      Expression

    1                            2,3       ~ dt       ( Tr A -> Tr suc A )
    2   suctr.1                            suctr.1  Tr A
    3   suctr.1                  4         ~ dftr2i   Tr suc A
    4   suctr.1                  5         ~ gen2     A. z A. y ( ( z e. y /\
                                                   y e. suc A ) -> z e. suc A )
    5   suctr.1                  6,7       ~ dt      ( ( z e. y /\ y e. suc A )
                                                    -> z e. suc A )
    6   suctr.1,suctr.2                    suctr.2  ( z e. y /\ y e. suc A )
    7   suctr.1,suctr.2          10,16,9   ~ jaoii    z e. suc A
    8   suctr.1,suctr.2          6         ~ pm3.27i  y e. suc A
    9   suctr.1,suctr.2          8         ~ elsucii  ( y e. A \/ y = A )
    10  suctr.1,suctr.2          11,12     ~ dt       ( y e. A -> z e. suc A )
    11  suctr.1,suctr.2,suctr.3            suctr.3  y e. A
    12  suctr.1,suctr.2,suctr.3  15,14     ~ sselii   z e. suc A
    13  suctr.1,suctr.2,suctr.3  6         ~ pm3.26i  z e. y
    14  suctr.1,suctr.2,suctr.3  2,13,11   ~ treli    z e. A
    15  suctr.1,suctr.2,suctr.3            ~ sssucid  A (_ suc A
    16  suctr.1,suctr.2          17,18     ~ dt       ( y = A -> z e. suc A )
    17  suctr.1,suctr.2,suctr.4            suctr.4  y = A
    18  suctr.1,suctr.2,suctr.4  21,20     ~ sselii   z e. suc A
    19  suctr.1,suctr.2,suctr.4  6         ~ pm3.26i  z e. y
    20  suctr.1,suctr.2,suctr.4  19,17     ~ eleqtri  z e. A
    21  suctr.1,suctr.2,suctr.4            ~ sssucid  A (_ suc A
       </PRE> $)
    suctrALT $p |- ( Tr A -> Tr suc A ) $=
      ( wtr csuc cv wcel wa wi wceq sssucid pm3.26i treli sselii dt eleqtri
      pm3.27i elsucii jaoii gen2 dftr2i ) CHCIZHDBAUFBJZAJZKZUHUFKZLZUGUFKZMBAU
      KULEUHCKZULUHCNZUMULFCUFUGCOZCUGUHDUIUJEPZFQRSUNULGCUFUGUOUGUHCUPGTRSUHCU
      IUJEUAUBUCSUDUES $.
      $( [19-Apr-2009] $) $( [24-Dec-2008] $)
  $}

  ${
    sbf3.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution has no effect on a bound variable. $)
    sbf3 $p |- ( [ y / x ] A. y ph <-> A. y ph ) $=
      ( wal wi ax-gen 19.20 ax-mp ax-7 syl sbf ) ACEZBCMABEZCEZMBEANFZCEMOFPCDG
      ANCHIACBJKL $.
      $( [15-Feb-2010] $)
  $}

  ${
    sbf5.1 $e |- ( ph -> A. x ph ) $.
    $( Substitution has no effect on a bound variable. $)
    sbf5 $p |- ( [ y / x ] E. y ph <-> E. y ph ) $=
      ( wex hbex sbf ) ACEBCABCDFG $.
      $( [15-Feb-2010] $)
  $}

  ${
    $( Substitution has no effect on a bound variable. $)
    sbf4 $p |- ( [ y / x ] E. x ph <-> E. x ph ) $=
      ( wex hbe1 sbf ) ABDBCABEF $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x y $.
    subdefbi1.1 $e |- ( E. x ( x = y /\ ph ) <-> ps ) $.
    $( Apply the definition of substitution to a biconditional. $)
    subdefbi1 $p |- ( [ y / x ] ph <-> ps ) $=
      ( cv wsbc wceq wa wex sb5 bitri ) ACDFZGCFMHAICJBACDKEL $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x z $. $d x y $.
    $( Lemma for ~ elsb3ALT . $)
    elsb3ALTlem $p |- ( E. x ( x = y /\ x e. z ) <-> y e. z ) $=
      ( cv wcel ax-17 elequ1 equsex ) ADCDZEBDIEZABJAFABCGH $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x z $. $d x y $.
    $(  Lemma for ~ elsb4ALT . $)
    elsb4ALTlem $p |- ( E. x ( x = y /\ z e. x ) <-> z e. y ) $=
      ( cv wcel ax-17 elequ2 equsex ) CDZADEIBDEZABJAFABCGH $.
      $( [15-Feb-2010] $)
  $}

  ${
    $( If two sets are equal they are not ordinary. $)
    elequ3 $p |- ( x = y -> ( x e. x <-> y e. y ) ) $=
      ( cv wceq wcel wb wa elequ1 elequ2 jca bitr syl ) ACZBCZDZMMEZNMEZFZQNNEZ
      FZGPSFORTABAHABBIJPQSKL $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x y $.
    $( Lemma for ~ elsb2 . $)
    elsb2lem $p |- ( E. x ( x = y /\ x e. x ) <-> y e. y ) $=
      ( cv wcel ax-17 elequ3 equsex ) ACZHDBCZIDZABJAEABFG $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x z $.  $d x y $.
    $( Substituion applied to an atomic membership wff.  ~ elsb3ALT is
       ~ elsb3 with the additional distinct variable requirement that ` x `
       and ` y ` are distinct, but with a shorter proof. $)
    elsb3ALT $p |- ( [ y / x ] x e. z <-> y e. z ) $=
      ( cv wcel elsb3ALTlem subdefbi1 ) ADCDZEBDHEABABCFG $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x z $.  $d x y $.
    $( Substitution applied to an atomic membership wff.  ~ elsb4ALT is
       ~ elsb4 with the additional distinct variable requirement that ` x `
       and ` y ` are distinct, but with a shorter proof. $)
    elsb4ALT $p |- ( [ y / x ] z e. x <-> z e. y ) $=
      ( cv wcel elsb4ALTlem subdefbi1 ) CDZADEHBDEABABCFG $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x y $.
    $( Substitution applied to the atomic membership wff that a set contains
       itself.  This wff is false by the axiom of regularity. $)
    elsb2 $p |- ( [ y / x ] x e. x <-> y e. y ) $=
      ( cv wcel elsb2lem subdefbi1 ) ACZGDBCZHDABABEF $.
      $( [15-Feb-2010] $)
  $}

  ${
    $d x z $. $d x w $.
    $( Substitution applied to an atomic membership wff in which ` x ` doesn't
       occur. $)
    elsb1 $p |- ( [ y / x ] z e. w <-> z e. w ) $=
      ( cv wcel ax-17 sbf ) CEDEFZABIAGH $.
      $( [15-Feb-2010] $)
  $}

$( (End of Alan Sare's mathbox.) $)
