$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       AC equivalents:  well ordering, Zorn's lemma
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)
  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` P~ A ` . $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for ~ numth . $)
    numthlem $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 wceq wn cfv wcel wi cpw wral wex wf1o con0 wrex pwex ac4cOLD
      cima cdif cres wa tfr2 crn copab wfun cvv tfrlem7 visset resfunexg mp2an
      fvex rneq difeq2 fveq2 3syl fvopab fveq1i df-ima difeq2i fveq2i 3eqtr4
      syl6eq eleq1d wss difss ssexi elpw mpbir eqeq1 negbid id eleq12d imbi12d
      rcla4v ax-mp imp syl5bir exp3a com12 r19.21aiv tfr1 tz7.49c f1oeq1
      cla4ev r19.22si 19.23aiv ) BMZNOZPZWQFMZQZWQRZSZBCTZUAZFUBAMZCEMZUCZEUBZA
      UDUEZBXDFCIUFUGXEXJFXECGXFUHZUIZNOZPZXFGQZXLRZSZAUDUAXFCGXFUJZUCZAUDUEXJX
      EXQAUDXFUDRZXEXQXTXEXNXPXTXPXLWTQZXLRZXEXNUKXTXOYAXLXTXOXRHQZYAABADEGHJKU
      LXRWQCXGUMZUIZWTQZOEBUNZQCXRUMZUIZWTQZYCYAEBXRYFYJGUOXFUPRXRUPRABDEGHJKUQ
      AURGXFUPUSUTZYIWTVAXGXROYDYHOYEYIOYFYJOXGXRVBYDYHCVCYEYIWTVDVEVFXRHYGLVGX
      LYIWTXKYHCGXFVHVIVJVKVLVMXEXNYBXLXDRZXEXNYBSZSYLXLCVNCXKVOZXLCXLCIYNVPVQV
      RXCYMBXLXDWQXLOZWSXNXBYBYOWRXMWQXLNVSVTYOXAYAWQXLWQXLWTVDYOWAWBWCWDWEWFWG
      WHWIWJACGABDEGHJKWKIWLXSXIAUDXHXSEXRYKXFCXGXRWMWNWOVEWPWE $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z w v u f g h A $.
    numth.1 $e |- A e. V $.
    $( Numeration theorem:  every set can be put into one-to-one correspondence
       with some ordinal (using AC).  Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vg vz vw vu vv vh cv wfn cfv cres crn cdif wceq copab wral wa con0
      wrex cab cuni rdglem1 eqid id rneq difeq2 fveq2 3syl eqeqan12rd cbvopabv
      numthlem ) AEBFLZGLZMHLZUPNUPUROILZBJLZPZQZKLZNZRZJISZNRHUQTUAGUBUCFUDZCK
      VGUEZVFDGHAEFCVFUFVHUGVEELZBCLZPZQZVCNZRJICEUSVIRZUTVJRZUSVIVDVMVNUHVOVAV
      KRVBVLRVDVMRUTVJUIVAVKBUJVBVLVCUKULUMUNUO $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z A $.
    $( Numeration theorem:  any set is equinumerous to some ordinal (using AC).
       Theorem 10.3  of [TakeutiZaring] p. 84. $)
    numth2 $p |- E. x e. On x ~~ A $=
      ( vy vz cvv wcel cv cen wbr con0 wrex wceq breq2 rexbidv wf1o wex visset
      numth bren rexbii mpbir vtoclg wn c0 0elon enrefg ax-mp brprc mpbiri
      breq1 rcla4ev mpan syl pm2.61i ) BEFZAGZBHIZAJKZUPCGZHIZAJKZURCBEUSBLUTUQ
      AJUSBUPHMNVAUPUSDGODPZAJKAUSDCQZRUTVBAJUPUSDVCSTUAUBUOUCZUDBHIZURVDVEUDUD
      HIZUDJFZVFUEUDJUFUGUDBHUHUIVGVEURUEUQVEAUDJUPUDBHUJUKULUMUN $.
      $( [20-Oct-2003] $) $( [20-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Any set is strictly dominated by some ordinal. $)
    numthcor $p |- ( A e. B -> E. x e. On A ~< x ) $=
      ( vy cv csdm wbr con0 wrex wceq breq1 rexbidv cpw cen numth2 visset pwex
      ensym canth2 cvv wcel wa wi sdomentr ax-mp mpan syl r19.22si vtoclg ) DEZ
      AEZFGZAHIZBUKFGZAHIDBCUJBJULUNAHUJBUKFKLUKUJMZNGZAHIUMAUOOUPULAHUPUOUKNGZ
      ULUKUOUJDPZQRUJUOFGZUQULUJURSUKTUAUSUQUBULUCAPUJUOUKTUDUEUFUGUHUEUI $.
      $( [11-Nov-2003] $) $( [22-Oct-2003] $)
  $}

  ${
    $d x y z w f A $.
    weth.1 $e |- A e. V $.
    $( Well-ordering theorem: any set ` A ` can be well-ordered.  This is an
       equivalent of the Axiom of Choice.  Theorem 6 of [Suppes] p. 242. $)
    weth $p |- E. x x We A $=
      ( vy vf vz vw cv wf1o wex con0 wrex wwe numth wcel wi ccnv f1ocnv cep
      cfv wbr copab eqid f1owe cxp cin weinxp xpex inex2 weeq1 cla4ev sylbi
      syl6 word eloni ordwe syl syl5 19.23aiv com12 r19.23aiv ax-mp ) DHZBEHZIZ
      EJZDKLBAHZMZAJZDBECNVFVIDKVFVCKOZVIVEVJVIPZEVEBVCVDQZIZVKVCBVDRVMVCSMZVIV
      JVMVNBFHVLTGHVLTSUAFGUBZMZVIFGBVCVOSVLVOUCUDVPBVOBBUEZUFZMZVIBVOUGVHVSAVR
      VQVOBBCCUHUIBVGVRUJUKULUMVJVCUNVNVCUOVCUPUQURUQUSUTVAVB $.
      $( [9-Mar-1997] $)
  $}

  ${
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.
    $d h t f G $.
    $d t C $.  $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zornlem.1 $e |- A e. V $.
    $( Hypotheses for transfinite recursion. $)
    zornlem.2 $e |- B = { f | E. h e. On ( f Fn h /\
                A. t e. h ( f ` t ) = ( G ` ( f |` t ) ) ) } $.
    zornlem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zornlem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zornlem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A ` . $)
    zornlem.6 $e |- G =
                { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.
    $( Lemma for ~ zorn . $)
    zornlem1 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con0 wcel wwe c0 wne wa cfv wbr wn wral crab cuni cres tfr2 wceq
      copab fveq1i wfun cvv tfrlem7 visset resfunexg mp2an cima ssrab2 eqsstr
      ssexi rabex uniex cab crn rneq df-ima syl6eqr eleq2d imbi1d ralbidv2
      rabbisdv 3eqtr4g anbi12d abbidv df-rab unieqd fvopab eqtr syl6eq eleq1d
      wreu wss wereu mp3an2 reucl syl syl5bir imp ) AUCZUDUEZGCUCZUFZJUGUHZUIZW
      SOUJZJUEZWTXFEUCZDUCZXAUKULZEJUMZDJUNZUOZJUEZXDWTXEXLJWTXEOWSUPZPUJZXLNFA
      HLOPRSUQXOXNFUCXIEIUMZDIUNZUOZURLFUSZUJXLXNPXSUBUTLFXNXRXLOVAWSVBUEXNVBUE
      NFHLOPRSVCAVDOWSVBVEVFXKXJDJJGQJMUCZBUCKUKZMOWSVGZUMZBGUNZGUAYCBGVHVIZVJZ
      VKVLLUCZXNURZXQXKYHXHIUEZXPUIZDVMXHJUEZXJUIZDVMXQXKYHYJYLDYHYIYKXPXJYHIJX
      HYHYAMYGVNZUMZBGUNYDIJYHYNYCBGYHYAYAMYMYBYHXTYMUEXTYBUEYAYHYMYBXTYHYMXNVN
      YBYGXNVOOWSVPVQVRVSVTWATUAWBZVRYHXIXIEIJYHXGIUEXGJUEXIYHIJXGYOVRVSVTWCWDX
      PDIWEXJDJWEWBWFWGWHWIWJXDXJDJWKZXMXBJGWLXCYPYEDEGJXAYFWMWNXJDJWOWPWQWR $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn . $)
    zornlem2 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con0 wcel wwe c0 wne wa cfv cima wbr wi cdm wss onsst wfn wceq tfr1
      fndm ax-mp syl6ssr wfun tfrlem7 funfvima2 mpan syl adantr wral zornlem1
      breq2 ralbidv elrab2 pm3.27bd breq1 rcla4cv 3syl syld ) AUDZUEUFZHDUDUGKU
      HUIUJZUJZBUDZVTUFZWDPUKZPVTULZUFZWFVTPUKZLUMZWAWEWHUNZWBWAVTPUOZUPZWKWAVT
      UEWLVTUQPUEURWLUEUSOGIMPQSTUTUEPVAVBVCPVDWMWKOGIMPQSTVEVTWDPVFVGVHVIWCWIK
      UFZNUDZWILUMZNWGVJZWHWJUNACDEFGHIJKLMNOPQRSTUAUBUCVKWNWIHUFWQWOCUDZLUMZNW
      GVJWQCWIHKWRWIUSWSWPNWGWRWIWOLVLVMUBVNVOWPWJNWFWGWOWFWILVPVQVRVS $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn . $)
    zornlem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ D =/= (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con0 wcel wwe c0 wne wa cfv wbr wceq wn wi zornlem2 adantl
      breq1 biimprcd poirr nsyli com12 zornlem1 cima wral crab ssrab2 eqsstr
      sseli syl sylan2 syld ) HLUDZAUEZUFUGHDUEUHKUIUJUKUKZUKBUEZVOUGZVQPULZVOP
      ULZLUMZVTVSUNZUOZVPVRWAUPVNABCDEFGHIJKLMNOPQRSTUAUBUCUQURVNVTHUGZWAWCUPVP
      WAVNWDUKZWCWAWBVTVTLUMZWEWBWFWAVTVSVTLUSUTHVTLVAVBVCVPVTKUGWDACDEFGHIJKLM
      NOPQRSTUAUBUCVDKHVTKNUECUELUMNPVOVEVFZCHVGHUBWGCHVHVIVJVKVLVM $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn . $)
    zornlem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa c0 wceq con0 wrex crn cvv wcel wn pm3.24 wne wi wal
      wss cfv ax-17 hba1 hban eleq1 zornlem1 wbr cima wral crab ssrab2 eqsstr
      sseli syl syl5bi com12 exp32 a2d a4sd imp r19.23ad wfn wb tfr1 fvelrn
      ax-mp syl5ib ssrdv ssex ex adantl ccnv wfun zornlem3 exp45 com23 imp4a
      19.21adv 19.20dv r2al syl6ibr cres ssid tz7.48lem mpan wrel cdm tfrlem6
      fndm eqimssi relssres mp2an cnveq funeq sylib syl6 onprc funrnex df-rn
      eleq1i dfdm4 eqtr3 3imtr4g mtoi jcad df-ne ralbii df-ral ralnex 3bitr3
      syl5ibr mt3i ) GKUDZGCUEUFZUGZJUHUIZAUJUKZOULZUMUNZYSUOZUGZYSUPYOAUEZUJUN
      ZJUHUQZURZAUSZUUAYQUOZYOUUFYSYTYNUUFYSURYMYNUUFYSYNUUFUGZYRGUTYSUUHUCYRGU
      UHUUBOVAZUCUEZUIZAUJUKZUUJGUNZUUJYRUNZUUHUUKUUMAUJYNUUFAYNAVBUUEAVCVDUUMA
      VBYNUUFUUCUUKUUMURZURZYNUUEUUPAYNUUCUUDUUOUUCYNUUDUUOURUUCYNUUDUUOUUKUUCY
      NUUDUGUGZUUMUUKUUIGUNZUUMUUQUUIUUJGVEUUQUUIJUNUURABCDEFGHIJKLMNOPQRSTUAUB
      VFJGUUIJMUEBUEKVGMOUUBVHVIZBGVJGUAUUSBGVKVLVMVNVOVPVQVPVRVSVTWAOUJWBZUUNU
      ULWCNFHLOPRSWDZAUJUUJOWEWFWGWHYRGQWIVNWJWKYOUUFOWLZWMZYTYOUUFUUIUUJOVAUIU
      OZUCUUBVIAUJVIZUVCYOUUFUUCUUJUUBUNZUGUVDURZUCUSZAUSUVEYOUUEUVHAYOUUEUVGUC
      YOUUEUUCUVFUVDYOUUCUUDUVFUVDURZYMYNUUCUUDUVIURZURYMUUCYNUVJYMUUCYNUUDUVIA
      UCBCDEFGHIJKLMNOPQRSTUAUBWNWOWPVTVRWQWRWSUVDAUCUJUUBWTXAUVEOUJXBZWLZWMZUV
      CUJUJUTUVEUVMUJXCAUCUJOUVAXDXEUVLUVBUIZUVMUVCWCUVKOUIZUVNOXFOXGZUJUTUVONF
      HLOPRSXHUVPUJUUTUVPUJUIUVAUJOXIWFZXJOUJXKXLUVKOXMWFUVLUVBXNWFXOXPUVCYSUJU
      MUNZXQUVCUVBXGZUMUNZUVBULZUMUNZYSUVRUVTUVCUWBUMUVBXRVPYRUVSUMOXSXTUJUWAUM
      UVPUJUWAUVQOYAYBXTYCYDXPYEUUDAUJVIYPUOZAUJVIUUFUUGUUDUWCAUJJUHYFYGUUDAUJY
      HYPAUJYIYJYKYL $.
      $( [3-Apr-1997] $)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y ` . $)
      zornlem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for ~ zorn . $)
      zornlem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) ->
            ( F " x ) (_ A ) $=
        ( vs cv wwe con0 wcel wa c0 wne wral cima cfv wceq wrex ax-17 hbra1
        hban wi wal eleq1 zornlem1 wbr crab ssrab2 eqsstr sseli syl syl5bi
        onelon sylani com12 exp43 com3r imp a2d a4sd df-ral syl5ib r19.23ad
        wfun tfrlem7 fvelima mpan syl5 ssrdv ) HDUGUHZAUGZUIUJZUKZRULUMZBWKUNZU
        KZUFPWKUOZHWPBUGZPUPZUFUGZUQZBWKURZWTHUJZWTWQUJZWPXAXCBWKWMWOBWMBUSWNBW
        KUTVAXCBUSWMWOWRWKUJZXAXCVBZVBZWMXEWNVBZBVCXGWOWMXHXGBWMXEWNXFWJWLXEWNX
        FVBZVBWLXEWJXIWLXEWJWNXFXAWLXEUKZWJWNUKZUKXCXAWRUIUJZXKXCXJXAWSHUJZXCXL
        XKUKZWSWTHVDXNWSRUJXMBCDEFGHIJRLMNOPQSTUAUBUEUDVERHWSRNUGCUGLVFNPWRUOUN
        ZCHVGHUEXOCHVHVIVJVKVLWKWRVMVNVOVPVQVRVSVTWNBWKWAWBVRWCPWDXDXBOGIMPQTUA
        WEBWTWKPWFWGWHWI $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn . $)
      zornlem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) -> R Or ( F " x ) ) ) $=
        ( vs vr vb va wpo cv wwe con0 wcel wa c0 wne wral cima wbr wceq w3o
        wor wss wi zornlem5 poss syl com12 wal cfv wex crab onelon anim12i
        anandis ex eqid zornlem2 adantll breq12 biimpcd syl6 com23 adantrrl
        imp eqeq12 fveq2 syl5bi adantl adantlr wb ancoms adantrrr 3orim123d
        word ordtri3or eloni syl2an syl5 exp31 com4r pm2.43i exp4a com3r a2d
        neeq1i ralbii imaeq2 raleq1d rabbisdv neeq1d rcla4cv anim12d sylbi
        imp4b 19.23advv wfun wfn tfr1 fnfun ax-mp wrex fvelima df-rex sylib
        an4 2exbii eeanv bitr sylibr 19.21aivv r2al a1i jcad df-so syl6ibr ) HL
        UJZHDUKULZAUKZUMUNZUOZRUPUQZBYTURZUOZPYTUSZLUJZUFUKZUGUKZLUTZUUHUUIVAZU
        UIUUHLUTZVBZUGUUFURUFUUFURZUOUUFLVCYRUUEUUGUUNUUEYRUUGUUEUUFHVDYRUUGVEA
        BCDEFGHIJKLMNOPQRSTUAUBUCUDUEVFUUFHLVGVHVIUUEUUNVEYRUUEUUHUUFUNZUUIUUFU
        NZUOZUUMVEZUGVJUFVJUUNUUEUURUFUGUUEUHUKZYTUNZUIUKZYTUNZUOZUUSPVKZUUHVAZ
        UVAPVKZUUIVAZUOZUOZUIVLUHVLZUUMUUQUUEUVIUUMUHUIUUBUUDUVCUVHUUMUUBUVCNUK
        CUKLUTZNPUUSUSZURZCHVMZUPUQZUVKNPUVAUSZURZCHVMZUPUQZUOZVEZUVCUVHUUMVEZV
        EUUDUUBUVCUVTUWBYSUUAUVCUVTUWBVEZVEUUAUVCYSUWCUUAUVCYSUVTUWBUUAUVCUUSUM
        UNZUVAUMUNZUOZYSUVTUOZUWBVEZUUAUVCUWFUUAUUTUVBUWFUUAUUTUOUWDUUAUVBUOUWE
        YTUUSVNYTUVAVNVOVPVQUWFUWHUWFUWGUVHUWFUUMUWFUWGUVHUWFUUMVEUWFUWGUOZUVHU
        OZUUSUVAUNZUUSUVAVAZUVAUUSUNZVBZUUMUWFUWJUWKUUJUWLUUKUWMUULUWIUVHUWKUUJ
        VEZUWFYSUVSUVHUWOVEUVOUWFYSUVSUOZUOZUWKUVHUUJUWQUWKUVDUVFLUTZUVHUUJVEUW
        EUWPUWKUWRVEUWDUIUHCDEFGHIJUVRLMNOPQSTUAUBUVRVRUDVSVTUVHUWRUUJUVDUUHUVF
        UUILWAWBWCWDWEWFUVHUWLUUKVEUWIUVHUVDUVFVAUUKUWLUVDUUHUVFUUIWGUUSUVAPWHW
        IWJUWIUVHUWMUULVEZUWFYSUVOUVHUWSVEUVSUWFYSUVOUOZUOZUWMUVHUULUXAUWMUVFUV
        DLUTZUVHUULVEUWDUWTUWMUXBVEUWEUHUICDEFGHIJUVNLMNOPQSTUAUBUVNVRUDVSWKUVH
        UXBUULUVGUVEUXBUULWLUVFUUIUVDUUHLWAWMWBWCWDWNWFWOUUSWPUVAWPUWNUWDUWEUUS
        UVAWQUUSWRUVAWRWSWTXAXBXCWCXDXEWFXFUUDUVKNPBUKZUSZURZCHVMZUPUQZBYTURZUW
        AUUCUXGBYTRUXFUPUEXGXHUXHUUTUVOUVBUVSUXGUVOBUUSYTUXCUUSVAZUXFUVNUPUXIUX
        EUVMCHUXIUVKNUXDUVLUXCUUSPXIXJXKXLXMUXGUVSBUVAYTUXCUVAVAZUXFUVRUPUXJUXE
        UVQCHUXJUVKNUXDUVPUXCUVAPXIXJXKXLXMXNXOWTXPXQUUQUUTUVEUOZUHVLZUVBUVGUOZ
        UIVLZUOZUVJPXRZUUQUXOVEPUMXSUXPOGIMPQTUAXTUMPYAYBUXPUUOUXLUUPUXNUXPUUOU
        XLUXPUUOUOUVEUHYTYCUXLUHUUHYTPYDUVEUHYTYEYFVQUXPUUPUXNUXPUUPUOUVGUIYTYC
        UXNUIUUIYTPYDUVGUIYTYEYFVQXNYBUVJUXKUXMUOZUIVLUHVLUXOUVIUXQUHUIUUTUVBUV
        EUVGYGYHUXKUXMUHUIYIYJYKWTYLUUMUFUGUUFUUFYMYKYNYOUFUGUUFLYPYQ $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn . $)
      zornlem7 $p |- ( ( R Po A /\
      A. s ( ( s (_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
        ( wpo cv wss wor wa wbr wceq wo wral wrex wi wal wwe wex wn weth c0
        con0 wne zornlem4 cima crab imaeq2 raleq1d rabbisdv 3eqtr4g eqeq1d
        onminex df-ne ralbii anbi2i rexbii sylibr wcel zornlem5 a1i zornlem6
        jcad wfun cvv tfrlem7 visset funimaex ax-mp sseq1 soeq2 anbi12d raleq1
        rexbidv imbi12d cla4v sylan9 adantld imp noel sseld w3a potr 3anass
        sylan2br exp3a com23 breq1 biimprcd adantl jaod exp42 sylan9r com24
        imp31 a2d 19.20dv df-ral 3imtr4g wb eqeq1i eleq2 sylbi breq2 ralbidv
        elrab syl5bbr biimpd cbvralv syl5ib exp32 com34 mtoi exp4a ex com4r
        pm2.43a imp3a com4l 19.21adv syl6ibr imdistand 19.22dv df-rex com12
        adantr imp32 mpd exp45 imp4a com3l r19.23aiv 3syl adantlr pm2.43i
        19.23adv mpi ) HLUJZSUKZHULZUVCLUMZUNZTUKZUAUKZLUOZUVGUVHUPZUQZTUVCURZU
        AHUSZUTZSVAZUNZHDUKVBZDVCUVHUBUKZLUOZVDZUBHURZUAHUSZDHUCVEUVPUVQUWBDUVP
        UVQUWBUVPUVQUNZUWBUVBUVQUWCUWBUTZUVOUVBUVQUNKVFUPZAVGUSZUWERVFVHZBAUKZU
        RZUNZAVGUSZUWDACDEFGHIJKLMNOPQUCUDUEUFUGUHVIUWFUWERVFUPZVDZBUWHURZUNZAV
        GUSUWKUWEUWLABUWHBUKZUPZKRVFUWQNUKZCUKZLUOZNPUWHVJZURZCHVKZUWTNPUWPVJZU
        RZCHVKKRUWQUXBUXECHUWQUWTNUXAUXDUWHUWPPVLVMVNUGUIVOVPVQUWJUWOAVGUWIUWNU
        WEUWGUWMBUWHRVFVRVSVTWAWBUWJUWDAVGUWCUWHVGWCZUWJUWBUWCUXFUWEUWIUWBUVPUV
        QUXFUWEUWIUWBUTZUTZUTUVPUVQUXFUXHUVPUWEUVQUXFUNZUXGUVPUWEUXIUWIUWBUVPUW
        EUXIUWIUNZUNZUNUVKTUXAURZUAHUSZUWBUVPUXKUXMUVPUXJUXMUWEUVBUXJUXAHULZUXA
        LUMZUNZUVOUXMUVBUXJUXNUXOUXJUXNUTUVBABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWDZ
        WEABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWFWGUVNUXPUXMUTSUXAPWHUXAWIWCOGIMPQUD
        UEWJPUWHAWKWLWMUVCUXAUPZUVFUXPUVMUXMUXRUVDUXNUVEUXOUVCUXAHWNUVCUXALWOWP
        UXRUVLUXLUAHUVKTUVCUXAWQWRWSWTXAXBXCUVPUWEUXJUXMUWBUTZUVBUWEUXJUXSUTZUT
        UVOUWEUVBUXTUWEUVBUXJUXSUWEUVBUXJUNZUNZUVHHWCZUXLUNZUAVCUYCUWAUNZUAVCUX
        MUWBUYBUYDUYEUAUYBUYCUXLUWAUYBUYCUXLUWAUYBUYDUVRHWCZUVTUTZUBVAUWAUYBUYD
        UYGUBUYBUYCUXLUYGUYFUYBUYCUXLUVTUYFUWEUYAUYCUXLUVTUTZUTZUWEUYFUYAUYIUTU
        WEUYFUYAUYFUYIUWEUYFUYAUYFUYIUTUTUWEUYFUNZUYAUYCUYFUYHUYJUYAUYCUYFUYHUY
        JUYAUYCUYFUNZUXLUVTUYJUYAUYKUNZUNUXLUNUVSUVRVFWCZUVRXDUYJUYLUXLUVSUYMUT
        UYJUYLUVSUXLUYMUYJUYLUVSUXLUYMUTUYLUVSUNZUXLUVGUVRLUOZTUXAURZUYJUYMUYNU
        VGUXAWCZUVKUTZTVAUYQUYOUTZTVAUXLUYPUYNUYRUYSTUYNUYQUVKUYOUYAUYKUVSUYQUV
        KUYOUTZUTZUYAUVSUYKVUAUYAUYQUYKUVSUYTUXJUYQUVGHWCZUVBUYKUVSUYTUTUTUXJUX
        AHUVGUXQXEUVBVUBUYKUVSUYTUVBVUBUYKUNZUNZUVSUNUVIUYOUVJVUDUVSUVIUYOUTVUD
        UVIUVSUYOVUDUVIUVSUYOUVBVUBUYCUYFXFUVIUVSUNUYOUTVUCHUVGUVHUVRLXGVUBUYCU
        YFXHXIXJXKXCUVSUVJUYOUTVUDUVJUYOUVSUVGUVHUVRLXLXMXNXOXPXQXRXKXSXTYAUVKT
        UXAYBUYOTUXAYBYCUYJUWRUVRLUOZNUXAURZUYMUYPUWEUYFVUFUYMUTUWEUYFVUFUYMUWE
        UYFVUFUNZUYMUWEUVRUXCWCZUYMVUGUWEUXCVFUPVUHUYMYDKUXCVFUGYEUXCVFUVRYFYGU
        XBVUFCUVRHUWSUVRUPUWTVUENUXAUWSUVRUWRLYHYIYJYKYLXJXCUYOVUETNUXAUVGUWRUV
        RLXLYMYNXQYOYPXSYQXPYRYPYSYTUUAUUBUUCUUBUUDUVTUBHYBUUEXJUUFUUGUXLUAHUUH
        UWAUAHUUHYCYOUUIUUJUUKUULUUMXKXJXCUUNUUOUUPUUQUURUUSYSUUTUVA $.
        $( [6-Apr-1997] $)
    $}

  $}

  ${
    $d x y z $.  $d x y w $.
    $( Lemma for ~ zorn2 . $)
    zorn2lem $p |- ( z { <. x , y >. | x (. y } w <-> z (. w ) $=
      ( cv wpss copab visset psseq1 psseq2 eqid brab ) AEZBEZFZCEZNFPDEZFABPQOA
      BGZCHDHMPNINQPJRKL $.
      $( [16-Aug-2004] $) $( [13-Aug-2004] $)
  $}

  ${
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
    zorn.1 $e |- A e. V $.
    $( Zorn's Lemma of [Monk1] p. 117.  This theorem is equivalent to the
       Axiom of Choice and states that every partially ordered set ` A ` (with
       an ordering relation ` R ` ) in which every totally ordered subset has
       an upper bound, contains at least one maximal element.  The main proof
       consists of lemmas ~ zornlem1 through ~ zornlem7 ; this final piece
       mainly changes bound variables to eliminate the hypotheses of
       ~ zornlem7 . $)
    zorn $p |- ( ( R Po A /\
      A. w ( ( w (_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( vt vu vr vq vn vj vg va vb vc vk vm vv vh vd vs vf cv wfn cfv cres wbr
      wn crn wral crab cuni wceq copab wa con0 wrex cab cima rdglem1 eqid
      breq2 ralbidv breq1 cbvralv syl5bb cbvrabv id wcel rneq raleq1d rabbisdv
      eleq2d wb raleq1 negbid syl anbi12d abbidv eleq1 cbvabv syl5eq df-rab
      3eqtr4g unieqd eqeqan12rd cbvopabv zornlem7 ) HIJKLMNEOUEZPUEZUFQUEZWKUGW
      KWMUHRUEZWNSUEZKUEZUIZUJZRWPTUEZFUIZKUAUEZUKZULZTEUMZULZSXDUMZUNZUOZUARUP
      ZUGUOQWLULUQPURUSOUTZWTKUBUEZUKZULZTEUMZUCUEZJUEZFUIZUCXJUNZHUEVAULJEUMZF
      UBUCUDXRXIXQUCXRIUEVAULJEUMZDCABGPQUDNOUBXIVBXRVCXMXQUCXLULZTJEWSXPUOZXOW
      SFUIZUCXLULYAXMYBYCXQUCXLWSXPXOFVDVEWTYCKUCXLWPXOWSFVFVGVHVIXSVCXHNUEZMUE
      ZLUEZWPUIZUJZMXNULZLXNUMZUNZUOUARUBNWNYDUOZXAXKUOZWNYDXGYKYLVJYMXFYJYMWOX
      DVKZXEUQZSUTZYFXNVKZYIUQZLUTZXFYJYMYFXDVKZWNYFWPUIZUJZRXDULZUQZLUTYSYPYMU
      UDYRLYMYTYQUUCYIYMXDXNYFYMXCXMTEYMWTKXBXLXAXKVLVMVNZVOYMXDXNUOZUUCYIVPUUE
      UUFYHMXDULYIUUCYHMXDXNVQUUBYHRMXDWNYEUOUUAYGWNYEYFWPVFVRVGVHVSVTWAYOUUDSL
      WOYFUOZYNYTXEUUCWOYFXDWBUUGWRUUBRXDUUGWQUUAWOYFWNWPVDVRVEVTWCWDXESXDWEYIL
      XNWEWFWGWHWIXTVCWJ $.
      $( [6-Apr-1997] $)

    $( Zorn's Lemma.  If the union of every chain (with respect to inclusion)
       in a set belongs to the set, then the set contains a maximal element.
       This theorem is equivalent to the Axiom of Choice.  Theorem 6M of
       [Enderton] p. 151. $)
    zorn2 $p |- ( A. z ( ( z (_ A /\
                  A. x e. z A. y e. z ( x (_ y \/ y (_ x ) ) -> U. z e. A ) ->
                E. x e. A A. y e. A -. x (. y ) $=
      ( vw vv vu cv wss wo wral wa cuni wcel wi wal wpss copab wbr wn wrex wor
      wceq w3o wpo df-so pm3.27bd zorn2lem pm4.2 3orbi123i sspsstri bitr4
      2ralbii sylib anim2i risset eqimss2 unissb orbi1i sspss ralbii sylibr
      r19.22si sylbi imim12i 19.20i w3a pssirr mtbir psstr syl2anb pm3.2i a1i
      rgen3 df-po mpbir zorn mpan syl negbii rexbii ) CIZDJZAIZBIZJWFWEJKZBWCLA
      WCLZMZWCNZDOZPZCQZWEWFFIGIRFGSZTZUAZBDLZADUBZWEWFRZUAZBDLZADUBWMWDWCWNUCZ
      MZHIZWEWNTZXDWEUDZKZHWCLZADUBZPZCQZWRWLXJCXCWIWKXIXBWHWDXBWOWEWFUDZWFWEWN
      TZUEZBWCLAWCLZWHXBWCWNUFXOABWCWNUGUHXNWGABWCWCXNWSXLWFWERZUEWGWOWSXLXLXMX
      PFGABUIZXLUJFGBAUIZUKWEWFULUMUNUOUPWKWEWJUDZADUBXIAWJDUQXSXHADXSXDWEJZHWC
      LZXHXSWJWEJYAWJWEURHWCWEUSUOXGXTHWCXGXDWERZXFKXTXEYBXFFGHAUIZUTXDWEVAUMVB
      VCVDVEVFVGDWNUFZXKWRYDXDXDWNTZUAZXDWFWNTZXMMXEPZMZADLBDLHDLYIHBADYIXDDOWF
      DOWEDOVHYFYHYEXDXDRXDVIFGHHUIVJXDWFRZXPXEYGXMYJXPMYBXEXDWFWEVKYCVCFGHBUIX
      RVLVMVNVOHBADWNVPVQABHCDWNEVRVSVTWQXAADWPWTBDWOWSXQWAVBWBUO $.
      $( [16-Aug-2004] $) $( [12-Aug-2004] $)
  $}

  ${
     $d f F $.  $d f A $.  $d f B $.
     fodom.1 $e |- A e. V $.
     $( An onto function implies dominance of range over domain.  Lemma 10.20
        of [Kunen] p. 30.  This theorem uses the Axiom of Choice ~ ac7g .  AC
        is not needed for finite sets - see ~ fodomfi . $)
     fodom $p |- ( F : A -onto-> B -> B ~<_ A ) $=
       ( vf wfo cv ccnv wss cdm wfn wa wex cdom wbr cvv wcel wf fof fex mpan2
       syl cnvexg ac7g 3syl wi wceq wb crn forn df-rn syl5eqr fneq2 domtr wf1
       wfun fnfrn biimp ad2antlr funss impcom ffun cnvss cnvcnvss sstr syl2an
       adantlr jca df-f1 sylibr visset rnexg ax-mp f1dom2g rnss adantl fdm
       dfdm4 adantr sseqtrd ssdomg sylanc exp31 sylbid com23 imp3a 19.23adv
       mpd ) ABCFZEGZCHZIZWJWKJZKZLZEMZBANOZWICPQZWKPQWPWIABCRZWRABCSZWSAPQWRDA
       BPCTUAUBCPUCPWKEUDUEWIWOWQEWIWLWNWQWIWNWLWQWIWNWJBKZWLWQUFWIWMBUGWNXAUHW
       ICUIBWMABCUJCUKULWMBWJUMUBWIXAWLWQBWJUIZNOZXBANOZWQWIXALWLLZBXBAUNXEBXBW
       JUOZXCXEBXBWJRZWJHZUPZLXFXEXGXIXAXGWIWLXAXGBWJUQURUSWIWLXIXACUPZXHCIZXIW
       IWLXKXJXIXHCUTVAWIWSXJWTABCVBUBWLXHWKHZIZXKWJWKVCXMXLCIXKCVDXHXLCVEUAUBV
       FVGVHBXBWJVIVJXBPQZXFXCUFWJPQXNEVKWJPVLVMZBXBPWJVNVMUBWIWLXDXAWIWLLZXBAI
       ZXDXPXBWKUIZAWLXBXRIWIWJWKVOVPWIXRAUGWLWICJZAXRWIWSXSAUGWTABCVQUBCVRULVS
       VTXNXQXDUFXOXBAPWAVMUBVGWBWCWDWEWFWGWH $.
       $( [30-Jul-2004] $) $( [23-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( An onto function implies dominance of range over domain. $)
    fodomg $p |- ( A e. C -> ( F : A -onto-> B -> B ~<_ A ) ) $=
      ( vx cv wfo cdom wbr wi wceq foeq2 breq2 imbi12d visset fodom vtoclg ) EF
      ZBDGZBRHIZJABDGZBAHIZJEACRAKSUATUBRABDLRABHMNRBDEOPQ $.
      $( [30-Jul-2004] $) $( [23-Jul-2004] $)
  $}

  ${
    $d f g z A $.  $d f g z B $.
    fodomb.1 $e |- A e. V $.
    $( Equivalence of an onto mapping and dominance for a non-empty set.
       Proposition 10.35 of [TakeutiZaring] p. 93. $)
    fodomb $p |- ( ( A =/= (/) /\ E. f f : A -onto-> B ) <->
                 ( (/) ~< B /\ B ~<_ A ) ) $=
      ( c0 wne cv wfo wex wa csdm wbr cdom cdm wceq wf fof fdm syl eqeq1d crn
      forn dm0rn0 syl5bb bitr3d eqneqd biimpac wb cvv wcel wi fornex ax-mp
      0sdomg adantl mpbird ex fodom a1i jcad 19.23adv imp sdomdomtr 0sdom
      sylib fodomr mp3an1 jca impbi ) AEFZABCGZHZCIZJEBKLZBAMLZJZVJVMVPVJVLVPCV
      JVLVNVOVJVLVNVJVLJVNBEFZVLVJVQVLAEBEVLVKNZEOZAEOBEOZVLVRAEVLABVKPVRAOABVK
      QABVKRSTVLVKUAZEOVTVSVLWABEABVKUBTVKUCUDUEUFUGVLVNVQUHZVJVLBUIUJZWBAUIUJZ
      VLWCUKDABUIVKULUMBUIUNSUOUPUQVLVOUKVJABVKDURUSUTVAVBVPVJVMVPEAKLZVJWDVPWE
      UKDEBAUIVCUMADVDVEWDVNVOVMDABUICVFVGVHVI $.
      $( [24-Feb-2007] $) $( [29-Jul-2004] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    brdom3.1 $e |- A e. V $.
    brdom3.2 $e |- B e. V $.
    $( Equivalence to a dominance relation. $)
    brdom3 $p |- ( A ~<_ B <-> E. f ( A. x E* y x f y /\
                 A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wal wrex wral wa wex c0 wceq wfo wo wn wi csdm cvv
      wcel fodomr mp3an1 wne 0sdom df-ne bitr2 sylanb ancoms pm5.6 mpbi rzal
      cop noel df-br mtbir nex exmo ori ax-mp ax-gen jctil 0ex ax-17 breq
      mobid albidv rexbidv ralbidv anbi12d cla4ev syl wf wfun fof ffun wrel
      dffunmo pm3.27bd 3syl dffo4 jca 19.22i jaoi cxp cin cdm wfn crn inss1
      ssbri immoi 19.20i relxp relin2 mpbiran sylibr funfn sylib rninxp biimpr
      anim12i df-fo visset inex1 dmexg fodom wss inss2 dmss dmxpss sstri
      ssdom2g mp2 domtr mpan2 19.23aiv impbi ) CDHIZAJZBJZEJZIZBKZALZYEYDYFIZBD
      MZACNZOZEPZYCCQRZDCYFSZEPZTZYNYCYOUAZOYQUBYCYRUBYSYCYQQCUCIZYCYQYSDUDUEZY
      TYCYQGDCUDEUFUGYTCQUHYSCFUICQUJUKULUMYCYOYQUNUOYOYNYQYOYDYEQIZBKZALZYEYDQ
      IZBDMZACNZOZYNYOUUGUUDUUFACUPUUCAUUBBPZUAUUCUUBBUUBYDYEUQZQUEUUJURYDYEQUS
      UTVAUUIUUCUUBBVBVCVDVEVFYMUUHEQVGYFQRZYIUUDYLUUGUUKYHUUCAUUKYGUUBBUUKBVHY
      DYEYFQVIVJVKUUKYKUUFACUUKYJUUEBDYEYDYFQVIVLVMVNVOVPYPYMEYPYIYLYPDCYFVQZYF
      VRZYIDCYFVSDCYFVTUUMYFWAYIABYFWBWCWDYPUULYLBADCYFWEWCWFWGWHVPYMYCEYMYFDCW
      IZWJZWKZCUUOSZCUUPHIZYCYMUUOUUPWLZUUOWMCRZOUUQYIUUSYLUUTYIUUOVRZUUSYIYDYE
      UUOIZBKZALZUVAYHUVCAUVBYGBUUOYFYDYEYFUUNWNWOWPWQUVAUUOWAZUVDABUUOWBUUNWAU
      VEDCWRYFUUNWSVDWTXAUUOXBXCUUTYLBADCYFXDXEXFUUPCUUOXGXAUUPCUUOUUOUDUEUUPUD
      UEYFUUNEXHXIUUOUDXJVDXKUURUUPDHIZYCUUAUUPDXLUVFGUUPUUNWKZDUUOUUNXLUUPUVGX
      LYFUUNXMUUOUUNXNVDDCXOXPUUPDUDXQXRCUUPDXSXTWDYAYB $.
      $( [27-Mar-2007] $) $( [27-Mar-2007] $)
  $}

  ${
    $d f g x y A $.  $d f g x y B v w z $.
    brdom3reg.1 $e |- A e. V $.
    brdom3reg.2 $e |- B e. V $.
    $( An equivalence to dominance based on the ordered pair definition
       implied by ~ opthreg . $)
    brdom3regOLD $p |- ( A ~<_ B <-> E. f ( A. x E* y { x , { x , y } } e. f /\
                 A. x e. A E. y e. B { y , { y , x } } e. f ) ) $=
      ( vg vv vz vw cdom wbr cv wmo wal wrex wral wa wex cpr wcel brdom3 wceq
      cop cab cdm cvv visset dmexg ax-mp opeldm adantl 19.23aiv crn rnexg
      opelrn csn snex pm3.26 ss2abi df-sn sseqtr4 ssexi abexex ax-17 eleq2
      mobid albidv rexbidv ralbidv anbi12d cla4ev prex eqeq1 anbi1d 2exbidv
      elab opth opthreg bitr4 df-br anbi12i 2exbii wb breq12 copsex2g mp2an
      3bitr2 mobii albii rexbii ralbii syl2anbr copab df-opab cuni uniex pri1
      elunii mpan zfpair pri2 syl eqeltrr eqeltr breq preq1 preq2 eqtrd eleq1d
      eqid brab impbi bitr ) CDLMANZBNZHNZMZBOZAPZXQXPXRMZBDQZACRZSZHTZXPXPXQUA
      ZUAZENZUBZBOZAPZXQXQXPUAZUAZYIUBZBDQZACRZSZETZABCDHFGUCYFYSYEYSHYHINZJNZU
      UAKNZUAZUAZUDZUUAUUBUEZXRUBZSZKTZJTZIUFZUBZBOZAPZYNUUKUBZBDQZACRZYSYAYDYR
      UUNUUQSEUUKUUIJIXRUGZXRUHUBZUURUHUBHUIZXRUHUJUKUUHUUAUURUBZKUUGUVAUUEUUAU
      UBXRJUIZULUMUNUUHKIXRUOZUUSUVCUHUBUUTXRUHUPUKUUGUUBUVCUBUUEUUAUUBXRUVBKUI
      ZUQUMUUHIUFZUUDURZUUDUSUVEUUEIUFUVFUUHUUEIUUEUUGUTVAIUUDVBVCVDVEVEYIUUKUD
      ZYLUUNYQUUQUVGYKUUMAUVGYJUULBUVGBVFYIUUKYHVGVHVIUVGYPUUPACUVGYOUUOBDYIUUK
      YNVGVJVKVLVMUUMXTAUULXSBUULYHUUDUDZUUGSZKTJTZXPXQUEUUFUDZUUAUUBXRMZSZKTJT
      ZXSUUJUVJIYHXPYGVNYTYHUDZUUHUVIJKUVOUUEUVHUUGYTYHUUDVOVPVQVRUVMUVIJKUVKUV
      HUVLUUGUVKXPUUAUDXQUUBUDSUVHXPXQUUAUUBAUIZBUIZUVDVSXPXQUUAUUBUVPUVQUVBUVD
      VTWAUUAUUBXRWBZWCWDXPUHUBZXQUHUBZUVNXSWEUVPUVQUVLXSJKXPXQUHUHUUAXPUUBXQXR
      WFWGWHWIWJWKUUPYCACUUOYBBDUUOYNUUDUDZUUGSZKTJTZXQXPUEUUFUDZUVLSZKTJTZYBUU
      JUWCIYNXQYMVNYTYNUDZUUHUWBJKUWGUUEUWAUUGYTYNUUDVOVPVQVRUWEUWBJKUWDUWAUVLU
      UGUWDXQUUAUDXPUUBUDSUWAXQXPUUAUUBUVQUVPUVDVSXQXPUUAUUBUVQUVPUVBUVDVTWAUVR
      WCWDUVTUVSUWFYBWEUVQUVPUVLYBJKXQXPUHUHUUAXQUUBXPXRWFWGWHWIWLWMWNUNYRYFEXP
      XQUUDYIUBZJKWOZMZBOZAPZXQXPUWIMZBDQZACRZYFYLYQYEUWLUWOSHUWIUWIYTUUFUDZUWH
      SZKTZJTIUFUHUWHJKIWPUWRJIYIWQZYIEUIWRZUWQUUAUWSUBZKUWHUXAUWPUUAUUDUBUWHUX
      AUUAUUCUVBWSUUAUUDYIWTXAUMUNUWQKIUWSWQZUWSUWTWRUWHUUBUXBUBZUWPUWHUUCUWSUB
      ZUXCUUCUUDUBUWHUXDUUAUUCJKXBXCUUCUUDYIWTXAUUBUUCUBUXDUXCUUAUUBUVDXCUUBUUC
      UWSWTXAXDUMUWQIUFUWPIUFZUUFURUXEUHIUUFVBUUFUSXEUWQUWPIUWPUWHUTVAVDVEVEXFX
      RUWIUDZYAUWLYDUWOUXFXTUWKAUXFXSUWJBUXFBVFXPXQXRUWIXGVHVIUXFYCUWNACUXFYBUW
      MBDXQXPXRUWIXGVJVKVLVMUWKYKAUWJYJBUWHXPXPUUBUAZUAZYIUBYJJKXPXQUWIUVPUVQUU
      AXPUDZUUDUXHYIUXIUUDXPUUCUAZUXHUUAXPUUCXHUXIUUCUXGUDUXJUXHUDUUAXPUUBXHUUC
      UXGXPXIXDXJXKUUBXQUDZUXHYHYIUXKUXGYGUDUXHYHUDUUBXQXPXIUXGYGXPXIXDXKUWIXLZ
      XMWJWKUWNYPACUWMYOBDUWHXQXQUUBUAZUAZYIUBYOJKXQXPUWIUVQUVPUUAXQUDZUUDUXNYI
      UXOUUDXQUUCUAZUXNUUAXQUUCXHUXOUUCUXMUDUXPUXNUDUUAXQUUBXHUUCUXMXQXIXDXJXKU
      UBXPUDZUXNYNYIUXQUXMYMUDUXNYNUDUUBXPXQXIUXMYMXQXIXDXKUXLXMWLWMWNUNXNXO $.
      $( [27-Mar-2007] $) $( [27-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom4.1 $e |- A e. V $.
    brdom4.2 $e |- B e. V $.
    $( An equivalence to a dominance relation. $)
    brdom5 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y x f y /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wral wrex wa wex wal brdom3 alral anim1i 19.22i sylbi
      cxp cin cdm wfo wfn crn wceq wfun wrel wcel wss inss2 dmss ax-mp dmxpss
      sstri sseli inss1 ssbri immoi imim12i r19.20i2 relxp relin2 jctil dffun6
      sylibr funfn sylib rninxp biimpr anim12i df-fo cvv visset inex1 dmexg
      fodom ssdom2g mp2 domtr mpan2 3syl 19.23aiv impbi ) CDHIZAJZBJZEJZIZBKZAD
      LZWIWHWJIBDMACLZNZEOZWGWLAPZWNNZEOWPABCDEFGQWRWOEWQWMWNWLADRSTUAWOWGEWOWJ
      DCUBZUCZUDZCWTUEZCXAHIZWGWOWTXAUFZWTUGCUHZNXBWMXDWNXEWMWTUIZXDWMWTUJZWHWI
      WTIZBKZAXALZNXFWMXJXGWLXIADXAWHXAUKWHDUKWLXIXADWHXAWSUDZDWTWSULXAXKULWJWS
      UMWTWSUNUODCUPUQZURXHWKBWTWJWHWIWJWSUSUTVAVBVCWSUJXGDCVDWJWSVEUOVFABWTVGV
      HWTVIVJXEWNBADCWJVKVLVMXACWTVNVHXACWTWTVOUKXAVOUKWJWSEVPVQWTVOVRUOVSXCXAD
      HIZWGDVOUKXADULXMGXLXADVOVTWACXADWBWCWDWEWF $.
      $( [5-Apr-2007] $) $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation. $)
    brdom4 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y ( y e. A /\ x f y ) /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wcel wa wmo wral wrex wex wal brdom3 moan 19.20i alral syl
      anim1i 19.22i sylbi cxp cin cdm wfo wfn crn wceq wfun wrel wss inss2
      dmss ax-mp dmxpss sstri sseli rnss rnxpss inss1 ssbri anim12i immoi
      imim12i r19.20i2 relxp relin2 jctil dffun8 sylibr funfn sylib rninxp
      biimpr df-fo cvv visset inex1 dmexg fodom ssdom2g mp2 domtr mpan2 3syl
      19.23aiv impbi ) CDHIZBJZCKZAJZWMEJZIZLZBMZADNZWMWOWPIBDOACNZLZEPZWLWQBMZ
      AQZXALZEPXCABCDEFGRXFXBEXEWTXAXEWSAQWTXDWSAWQWNBSTWSADUAUBUCUDUEXBWLEXBWP
      DCUFZUGZUHZCXHUIZCXIHIZWLXBXHXIUJZXHUKZCULZLXJWTXLXAXNWTXHUMZXLWTXHUNZWMX
      MKZWOWMXHIZLZBMZAXINZLXOWTYAXPWSXTADXIWOXIKWODKWSXTXIDWOXIXGUHZDXHXGUOZXI
      YBUOWPXGUPZXHXGUQURDCUSUTZVAXSWRBXQWNXRWQXMCWMXMXGUKZCYCXMYFUOYDXHXGVBURD
      CVCUTVAXHWPWOWMWPXGVDVEVFVGVHVIXGUNXPDCVJWPXGVKURVLABXHVMVNXHVOVPXNXABADC
      WPVQVRVFXICXHVSVNXICXHXHVTKXIVTKWPXGEWAWBXHVTWCURWDXKXIDHIZWLDVTKXIDUOYGG
      YEXIDVTWEWFCXIDWGWHWIWJWK $.
      $( [30-Mar-2007] $) $( [28-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom5idisj.1 $e |- A e. V $.
    brdom5idisj.2 $e |- B e. V $.
    $( MAY BE REVISED $)
    $( A condition implying a dominance relation. $)
    brdom5idisj $p |- ( E. f ( A. x e. B E* y
      { x , y } e. f /\ A. x e. A E. y e. B { y , x } e. f ) -> A ~<_ B ) $=
      ( vg vw vz vv cv cpr wcel wmo wral wrex wa wex wbr cdom copab cop wceq
      cab cvv df-opab cuni visset uniex pri1 elunii mpan adantl 19.23aiv pri2
      csn df-sn snex eqeltrr pm3.26 ss2abi ssexi abexex eqeltr ax-17 breq
      mobid ralbidv rexbidv anbi12d cla4ev preq1 eleq1d preq2 eqid brab mobii
      ralbii rexbii syl2anbr brdom5 sylibr ) ALZBLZMZELZNZBOZADPZWEWDMZWGNZBDQZ
      ACPZRZESWDWEHLZTZBOZADPZWEWDWPTZBDQZACPZRZHSZCDUATWOXDEWDWEILZJLZMZWGNZIJ
      UBZTZBOZADPZWEWDXITZBDQZACPZXDWJWNXCXLXORHXIXIKLXEXFUCZUDZXHRZJSZISKUEUFX
      HIJKUGXSIKWGUHZWGEUIUJZXRXEXTNZJXHYBXQXEXGNXHYBXEXFIUIUKXEXGWGULUMUNUOXRJ
      KXTYAXHXFXTNZXQXFXGNXHYCXEXFJUIUPXFXGWGULUMUNXRKUEXQKUEZXPUQYDUFKXPURXPUS
      UTXRXQKXQXHVAVBVCVDVDVEWPXIUDZWSXLXBXOYEWRXKADYEWQXJBYEBVFWDWEWPXIVGVHVIY
      EXAXNACYEWTXMBDWEWDWPXIVGVJVIVKVLXKWIADXJWHBXHWDXFMZWGNWHIJWDWEXIAUIZBUIZ
      XEWDUDXGYFWGXEWDXFVMVNXFWEUDYFWFWGXFWEWDVOVNXIVPZVQVRVSXNWMACXMWLBDXHWEXF
      MZWGNWLIJWEWDXIYHYGXEWEUDXGYJWGXEWEXFVMVNXFWDUDYJWKWGXFWDWEVOVNYIVQVTVSWA
      UOABCDHFGWBWC $.
      $( [?] $) $( [29-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom4disj.1 $e |- A e. V $.
    brdom4disj.2 $e |- B e. V $.
    brdom4disj.3 $e |- ( A i^i B ) = (/) $.
    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom4disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      ( y e. A /\ { x , y } e. f ) /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vg vv vz vw cdom wbr cv wcel wa wmo wral wrex wex cpr brdom4 wceq cop
      cab csn snex pm3.26 ss2abi df-sn sseqtr4 ssexi abrexex2 ax-17 eleq2
      anbi2d mobid ralbidv rexbidv anbi12d cla4ev wb wne cin c0 incom eqtr
      disjne mp3an1 visset opthpr syl equcom anbi12i syl6rbb df-br a1i
      rexbidva rexcom zfpair eqeq1 anbi1d 2rexbidv elab bitr4 adantr breq1
      breq2 ceqsrex2v bitrd pm5.32da ralbiia ancoms eqcom ancom 3bitr4g bicomi
      syl5bb syl2anbr 19.23aiv copab cvv df-opab cuni uniex pri1 elunii mpan
      adantl pri2 eqeltrr abexex eqeltr breq preq1 eleq1d preq2 eqid brab
      anbi2i mobii ralbii rexbii impbi bitr ) CDMNBOZCPZAOZYGIOZNZQZBRZADSZYGYI
      YJNZBDTZACSZQZIUAZYHYIYGUBZEOZPZQZBRZADSZYGYIUBZUUAPZBDTZACSZQZEUAZABCDIF
      GUCYSUUKYRUUKIYHYTJOZKOZLOZUBZUDZUUMUUNUEYJPZQZKDTZLCTZJUFZPZQZBRZADSZUUF
      UVAPZBDTZACSZUUKYNYQUUJUVEUVHQEUVAUUSLJCFUURKJDGUURJUFZUUOUGZUUOUHUVIUUPJ
      UFUVJUURUUPJUUPUUQUIUJJUUOUKULUMUNUNUUAUVAUDZUUEUVEUUIUVHUVKUUDUVDADUVKUU
      CUVCBUVKBUOUVKUUBUVBYHUUAUVAYTUPUQURUSUVKUUHUVGACUVKUUGUVFBDUUAUVAUUFUPUT
      USVAVBUVDYMADYIDPZUVCYLBUVLBUOUVLYHUVBYKUVLYHQUVBUUMYIUDZUUNYGUDZQZUUMUUN
      YJNZQZLCTZKDTZYKUVLUVBUVSVCYHUVLUVSYTUUOUDZUUQQZLCTZKDTZUVBUVLUVRUWBKDUVL
      UVQUWALCUVLUUNCPZQZUVOUVTUVPUUQUWEUVTYIUUMUDZYGUUNUDZQZUVOUWEYIUUNVDZUVTU
      WHVCDCVEZVFUDZUVLUWDUWIUWJCDVEVFDCVGHVHZDCYIUUNVIVJYIYGUUMUUNAVKZBVKZKVKZ
      LVKZVLVMUWFUVMUWGUVNAKVNBLVNVOVPUVPUUQVCUWEUUMUUNYJVQZVRVAVSUTUWCUWAKDTLC
      TZUVBUWAKLDCVTUUTUWRJYTABWAUULYTUDZUURUWALKCDUWSUUPUVTUUQUULYTUUOWBWCWDWE
      WFVPWGUVPYIUUNYJNYKKLYIYGDCUUMYIUUNYJWHUUNYGYIYJWIWJWKWLURWMUVGYPACYICPZU
      VFYOBDUWTYGDPZQUVFUUNYIUDZUUMYGUDZQZUVPQZKDTZLCTZYOUWTUVFUXGVCUXAUWTUUFUU
      OUDZUUQQZKDTZLCTZUXGUVFUWTUXJUXFLCUWTUXIUXEKDUWTUUMDPZQZUXHUXDUUQUVPUXMUU
      OUUFUDZUXCUXBQZUXHUXDUXMUUMYIVDZUXNUXOVCUXLUWTUXPUWKUXLUWTUXPUWLDCUUMYIVI
      VJWNUUMUUNYGYIUWOUWPUWNUWMVLVMUUFUUOWOUXBUXCWPWQUUQUVPVCUXMUVPUUQUWQWRVRV
      AVSUTUUTUXKJUUFBAWAUULUUFUDZUURUXILKCDUXQUUPUXHUUQUULUUFUUOWBWCWDWEWSWGUV
      PUUMYIYJNYOLKYIYGCDUUNYIUUMYJWIUUMYGYIYJWHWJWKVSWMWTXAUUJYSEYHYIYGUUNUUMU
      BZUUAPZLKXBZNZQZBRZADSZYGYIUXTNZBDTZACSZYSUUEUUIYRUYDUYGQIUXTUXTUULUUNUUM
      UEZUDZUXSQZKUAZLUAJUFXCUXSLKJXDUYKLJUUAXEZUUAEVKXFZUYJUUNUYLPZKUXSUYNUYIU
      UNUXRPUXSUYNUUNUUMUWPXGUUNUXRUUAXHXIXJXAUYJKJUYLUYMUXSUUMUYLPZUYIUUMUXRPU
      XSUYOUUNUUMUWOXKUUMUXRUUAXHXIXJUYJJUFUYIJUFZUYHUGUYPXCJUYHUKUYHUHXLUYJUYI
      JUYIUXSUIUJUMXMXMXNYJUXTUDZYNUYDYQUYGUYQYMUYCADUYQYLUYBBUYQBUOUYQYKUYAYHY
      IYGYJUXTXOUQURUSUYQYPUYFACUYQYOUYEBDYGYIYJUXTXOUTUSVAVBUYCUUDADUYBUUCBUYA
      UUBYHUXSYIUUMUBZUUAPUUBLKYIYGUXTUWMUWNUXBUXRUYRUUAUUNYIUUMXPXQUXCUYRYTUUA
      UUMYGYIXRXQUXTXSZXTYAYBYCUYFUUHACUYEUUGBDUXSYGUUMUBZUUAPUUGLKYGYIUXTUWNUW
      MUVNUXRUYTUUAUUNYGUUMXPXQUVMUYTUUFUUAUUMYIYGXRXQUYSXTYDYCWTXAYEYF $.
      $( [31-Mar-2007] $) $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom5disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      { x , y } e. f /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vg vv vz vw cdom wbr cv wmo wral wrex wa wex cpr wcel brdom5 wceq cop
      cab csn snex pm3.26 ss2abi df-sn sseqtr4 ssexi abrexex2 ax-17 eleq2
      mobid ralbidv rexbidv anbi12d cla4ev wi wal wne wb cin c0 incom eqtr
      disjne mp3an1 visset opthpr syl breq12 biimprd syl6bi imp3a ex adantrd
      r19.23advv zfpair eqeq1 anbi1d df-br anbi2i syl6bbr 2rexbidv elab syl5ib
      19.21aiv immo r19.20i ancoms eqcom ancom 3bitr4g bicomi a1i rexbidva
      syl5bb adantr breq2 breq1 ceqsrex2v bitrd ralbiia biimpr syl2an 19.23aiv
      copab cvv df-opab cuni uniex pri1 elunii mpan adantl pri2 eqeltrr abexex
      eqeltr breq preq1 eleq1d preq2 eqid brab mobii ralbii rexbii syl2anbr
      impbi bitr ) CDMNAOZBOZIOZNZBPZADQZYQYPYRNZBDRZACQZSZITZYPYQUAZEOZUBZBPZA
      DQZYQYPUAZUUHUBZBDRZACQZSZETZABCDIFGUCUUFUUQUUEUUQIUUGJOZKOZLOZUAZUDZUUSU
      UTUEYRUBZSZKDRZLCRZJUFZUBZBPZADQZUULUVGUBZBDRZACQZUUQUUAUUDUUPUVJUVMSEUVG
      UVELJCFUVDKJDGUVDJUFZUVAUGZUVAUHUVNUVBJUFUVOUVDUVBJUVBUVCUIUJJUVAUKULUMUN
      UNUUHUVGUDZUUKUVJUUOUVMUVPUUJUVIADUVPUUIUVHBUVPBUOUUHUVGUUGUPUQURUVPUUNUV
      LACUVPUUMUVKBDUUHUVGUULUPUSURUTVAYTUVIADYPDUBZUVHYSVBZBVCYTUVIVBUVQUVRBUV
      QUUGUVAUDZUUSUUTYRNZSZKDRLCRZYSUVHUVQUWAYSLKCDUVQUUTCUBZUWAYSVBZUUSDUBZUV
      QUWCUWDUVQUWCSZUVSUVTYSUWFUVSYPUUSUDYQUUTUDSZUVTYSVBUWFYPUUTVDZUVSUWGVEDC
      VFZVGUDZUVQUWCUWHUWICDVFVGDCVHHVIZDCYPUUTVJVKYPYQUUSUUTAVLZBVLZKVLZLVLZVM
      VNUWGYSUVTYPUUSYQUUTYRVOVPVQVRVSVTWAUVFUWBJUUGABWBUURUUGUDZUVDUWALKCDUWPU
      VDUVSUVCSUWAUWPUVBUVSUVCUURUUGUVAWCWDUVTUVCUVSUUSUUTYRWEZWFWGWHWIWJWKUVHY
      SBWLVNWMUVMUUDUVLUUCACYPCUBZUVKUUBBDUWRYQDUBZSUVKUUTYPUDZUUSYQUDZSZUVTSZK
      DRZLCRZUUBUWRUVKUXEVEUWSUWRUULUVAUDZUVCSZKDRZLCRZUXEUVKUWRUXHUXDLCUWRUXGU
      XCKDUWRUWESZUXFUXBUVCUVTUXJUVAUULUDZUXAUWTSZUXFUXBUXJUUSYPVDZUXKUXLVEUWEU
      WRUXMUWJUWEUWRUXMUWKDCUUSYPVJVKWNUUSUUTYQYPUWNUWOUWMUWLVMVNUULUVAWOUWTUXA
      WPWQUVCUVTVEUXJUVTUVCUWQWRWSUTWTUSUVFUXIJUULBAWBUURUULUDZUVDUXGLKCDUXNUVB
      UXFUVCUURUULUVAWCWDWHWIXAXBUVTUUSYPYRNUUBLKYPYQCDUUTYPUUSYRXCUUSYQYPYRXDX
      EXFWTXGXHXIXJUUPUUFEYPYQUUTUUSUAZUUHUBZLKXKZNZBPZADQZYQYPUXQNZBDRZACQZUUF
      UUKUUOUUEUXTUYCSIUXQUXQUURUUTUUSUEZUDZUXPSZKTZLTJUFXLUXPLKJXMUYGLJUUHXNZU
      UHEVLXOZUYFUUTUYHUBZKUXPUYJUYEUUTUXOUBUXPUYJUUTUUSUWOXPUUTUXOUUHXQXRXSXJU
      YFKJUYHUYIUXPUUSUYHUBZUYEUUSUXOUBUXPUYKUUTUUSUWNXTUUSUXOUUHXQXRXSUYFJUFUY
      EJUFZUYDUGUYLXLJUYDUKUYDUHYAUYFUYEJUYEUXPUIUJUMYBYBYCYRUXQUDZUUAUXTUUDUYC
      UYMYTUXSADUYMYSUXRBUYMBUOYPYQYRUXQYDUQURUYMUUCUYBACUYMUUBUYABDYQYPYRUXQYD
      USURUTVAUXSUUJADUXRUUIBUXPYPUUSUAZUUHUBUUILKYPYQUXQUWLUWMUWTUXOUYNUUHUUTY
      PUUSYEYFUXAUYNUUGUUHUUSYQYPYGYFUXQYHZYIYJYKUYBUUNACUYAUUMBDUXPYQUUSUAZUUH
      UBUUMLKYQYPUXQUWMUWLUUTYQUDUXOUYPUUHUUTYQUUSYEYFUUSYPUDUYPUULUUHUUSYPYQYG
      YFUYOYIYLYKYMXJYNYO $.
      $( [5-Apr-2007] $) $( [5-Apr-2007] $)
  $}

  $( An image of a function under a set is dominated by the set.  Proposition
     10.34 of [TakeutiZaring] p. 92. $)
  imadomg $p |- ( A e. B -> ( Fun F -> ( F " A ) ~<_ A ) ) $=
    ( wcel wfun cima cres cdm cdom wbr wa crn cvv wfo fodomg resfunexg dmexg
    syl funres funforn sylib adantr sylc df-ima syl5eqbr expcom domtr wss cin
    dmres inss1 eqsstr ssdom2g mpi sylan2 syld ) ABDZCEZCAFZCAGZHZIJZUSAIJZURUQ
    VBURUQKZUTLZVAUSIVAMDZVAVEUTNZVEVAIJVDVAVEMUTOVDUTMDVFCABPUTMQRURVGUQURUTEV
    GACSUTTUAUBUCCAUDUEUFVBUQVCVBVAAIJZVCUQUSVAAUGUQVAAUHVHVAACHZUIACAUJAVIUKUL
    VAABUMUNUOUFUP $.
    $( [20-Nov-2004] $) $( [23-Jul-2004] $)

  $( The range of a function is dominated by its domain. $)
  fnrndomg $p |- ( A e. B -> ( F Fn A -> ran F ~<_ A ) ) $=
    ( wcel crn wfo cdom wbr wfn fodomg fnforn syl5ib ) ABDACEZCFMAGHCAIAMBCJACK
    L $.
    $( [1-Oct-2004] $) $( [1-Sep-2004] $)

  ${
    $d f g h v w x y z A $.  $d f g h v w x y z B $.
    unidom.1 $e |- A e. V $.
    unidom.2 $e |- B e. V $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98. $)
    unidom $p |- ( A. x e. A x ~<_ B -> U. A ~<_ ( A X. B ) ) $=
      ( vf vh vy vz vg cv cfv wf1 wral wex wcel wa cuni cxp cdom wbr wi cvv
      uniex cop wceq id fveq2 eleq12d eleq1d anbi12d rcla4cv pm3.27 syl6
      adantl wf wb f1eq1 syl f1eq2 bitrd rcla4v f1f ffvrn expcom sylan9r com3r
      imp jcad opelxpi fveq1d eqeq2d f1fveq biimpd imp31 adantrr adantr pm3.26
      eleq2 biimprd sylan9 anim12d an1rs adantlll sylanc sylbird ex imp3a fvex
      opth syl5ib fveq2d eqtrd opeq12d impbid1 dom2d mpi 19.23adv 19.23aiv
      brdom ralbii ac6s3 sylbi eleq1 eluni biimp mprg a1i sylc ) AKZCXJFKZLZMZA
      BNZFOZXJXJGKZLZPZXQBPZQZABRZNZGOZYABCSZTUAZXJCTUAZABNZXNYCYEUBFXNYBYEGXNY
      BYEXNYBQZYAUCPYEBDUDZYHHIYAYDHKZXPLZYJYKXKLZLZUEZIKZXPLZYOYPXKLZLZUEZUCYH
      YJYAPZYKBPZYMCPZQYNYDPYHYTUUAUUBYBYTUUAUBXNYBYTYJYKPZUUAQZUUAXTUUDAYJYAXJ
      YJUFZXRUUCXSUUAUUEXJYJXQYKUUEUGXJYJXPUHZUIUUEXQYKBUUFUJUKULZUUCUUAUMUNUOX
      NYBYTUUBUBYBYTXNUUBYBYTUUDXNUUBUBUUGUUAXNYKCYLUPZUUCUUBUUAXNYKCYLMZUUHXMU
      UIAYKBXJYKUFZXMXJCYLMZUUIUUJXLYLUFXMUUKUQXJYKXKUHXJCXLYLURUSXJYKCYLUTVAVB
      ZYKCYLVCUNUUHUUCUUBYKCYJYLVDVEVFUNVGVHVIYKYMBCVJUNYHYTYOYAPZQZYNYSUFZYJYO
      UFZUQYHUUNQZUUOUUPUUQYKYPUFZYMYRUFZQUUPUUOUUQUURUUSUUPUUQUURUUSUUPUBUUQUU
      RQZUUSYMYOYLLZUFZUUPUURUVBUUSUQUUQUURUVAYRYMUURYOYLYQYKYPXKUHVKVLUOUUIUUC
      YOYKPZQZUVBUUPUBUUTUUIUVDQUVBUUPYKCYJYOYLVMVNUUQUUIUURYHYTUUIUUMXNYBYTUUI
      YBYTXNUUIYBYTUUDXNUUIUBZUUGUUAUVEUUCUULUOUNVGVOVPVQYBUUNUURUVDXNYBUURUUNU
      VDYBUURQZUUNUVDUVFYTUUCUUMUVCYBYTUUCUBUURYBYTUUDUUCUUGUUCUUAVRUNVQYBUUMYO
      YPPZUURUVCYBUUMUVGYPBPZQZUVGXTUVIAYOYAXJYOUFZXRUVGXSUVHUVJXJYOXQYPUVJUGXJ
      YOXPUHZUIUVJXQYPBUVKUJUKULUVGUVHVRUNUURUVCUVGYKYPYOVSVTWAWBVHWCWDWEWFWGWH
      YKYMYPYRYJXPWIYJYLWIYOYQWIWJWKUUPYKYPYMYRYJYOXPUHZUUPYMYJYQLYRUUPYJYLYQUU
      PYKYPXKUVLWLVKYJYOYQUHWMWNWOWGWPWQWGWRWSYGXJCJKZMZJOZABNXOYFUVOABXJCJEWTX
      AUVNXMAJBFDXJCUVMXLURXBXCYCYGXJUVMPZUVMBPZQZJOZYCAYAUVRXTAJYAGYIUVMXQUFUV
      PXRUVQXSUVMXQXJVSUVMXQBXDUKXBXJYAPUVSJXJBXEXFXGXHXI $.
      $( [27-Mar-2006] $) $( [25-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98. $)
    unidomg $p |- ( ( A e. C /\ B e. D /\ A. x e. A x ~<_ B ) ->
                   U. A ~<_ ( A X. B ) ) $=
      ( vz vy wcel cv cdom wbr wral cuni cxp wi wceq raleq1 unieq xpeq1
      breq12d imbi12d breq2 ralbidv xpeq2 breq2d visset unidom vtocl2g 3impia
      ) BDHCEHAIZCJKZABLZBMZBCNZJKZUJFIZJKZAGIZLZURMZURUPNZJKZOUQABLZUMBUPNZJKZ
      OULUOOGFBCDEURBPZUSVCVBVEUQAURBQVFUTUMVAVDJURBRURBUPSTUAUPCPZVCULVEUOVGUQ
      UKABUPCUJJUBUCVGVDUNUMJUPCBUDUEUAAURUPGUFFUFUGUHUI $.
      $( [29-Mar-2006] $) $( [25-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    uniimadom.1 $e |- A e. V $.
    uniimadom.2 $e |- B e. V $.
    $( An upper bound for the cardinality of the union of an image.
       Theorem 10.48 of [TakeutiZaring] p. 99. $)
    uniimadom $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vy cima cuni cxp cdom wbr wfun cv cfv wral wa domtr cvv wcel unidomg
      mp3an2 funimaex adantr wi wceq wrex fvelima ex breq1 biimpd r19.22si
      r19.36av syl syl6 com23 imp r19.21aiv sylanc imadomg ax-mp xpdom1 ) DBHZI
      ZVCCJZKLZVEBCJZKLZVDVGKLDMZANDOZCKLZABPZQZVDVEVGRVCSTZGNZCKLZGVCPZVFVMVNC
      STVQVFFGVCCSSUAUBVIVNVLDBEUCUDVMVPGVCVIVLVOVCTZVPUEVIVRVLVPVIVRVJVOUFZABU
      GZVLVPUEZVIVRVTAVOBDUHUIVTVKVPUEZABUGWAVSWBABVSVKVPVJVOCKUJUKULVKVPABUMUN
      UOUPUQURUSVIVHVLVIVCBKLZVHBSTVIWCUEEBSDUTVAVCBCEFVBUNUDUS $.
      $( [30-Mar-2006] $) $( [25-Mar-2006] $)
  $}

  ${
    $d x z A $.  $d x y z B $.  $d y z F $.
    uniimadomf.1 $e |- ( y e. F -> A. x y e. F ) $.
    uniimadomf.2 $e |- A e. V $.
    uniimadomf.3 $e |- B e. V $.
    $( An upper bound for the cardinality of the union of an image.
       Theorem 10.48 of [TakeutiZaring] p. 99.  This version of ~ uniimadom
       uses a bound-variable hypothesis in place of a distinct variable
       condition. $)
    uniimadomf $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vz wfun cv cfv cdom wbr wral cima cuni cxp uniimadom ax-17 wcel hbfv
      hbbr wceq fveq2 breq1d cbvral sylan2b ) EJIKZELZDMNZICOECPQCDRMNAKZELZDMN
      ZACOICDEGHSUNUKAICUNITABUJDMABUIEFBKZUIUAATUBUOMUAATUODUAATUCULUIUDUMUJDM
      ULUIEUEUFUGUH $.
      $( [3-Apr-2006] $) $( [26-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d y z C $.
    iundom.1 $e |- A e. V $.
    iundom.2 $e |- B e. V $.
    iundom.3 $e |- C e. V $.
    $( An upper bound for the cardinality of an indexed union.  ` C ` depends
       on ` x ` and should be thought of as ` C ( x ) ` . $)
    iundom $p |- ( A. x e. A C ~<_ B -> U_ x e. A C ~<_ ( A X. B ) ) $=
      ( vy vz cdom wbr wral cv wcel wceq wa copab cfv ciun cxp cvv fvopab2
      mpan2 breq1d ralbiia cima cuni wfun wfn eqid fnopab2 fnfun ax-mp hbopab1
      uniimadomf mpan iuneq2i funiunfvf eqtr3 syl5eqbr sylbir ) DCJKZABLAMZVCBN
      ZHMDOPZAHQZRZCJKZABLZABDSZBCTZJKVHVBABVDVGDCJVDDUANVGDOGAHBDUAUBUCZUDUEVI
      VFBUFUGZVKVJJVFUHZVIVMVKJKVFBUIVNAHBDVFGVFUJUKBVFULUMZAIBCVFVEAHIUNZEFUOU
      PABVGSZVJVMABVGDVLUQVNVQVMOVOAIBVFVPURUMUSUTVA $.
      $( [5-Apr-2006] $) $( [26-Mar-2006] $)
  $}

