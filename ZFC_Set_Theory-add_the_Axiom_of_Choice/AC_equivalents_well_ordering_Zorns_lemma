$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
       AC equivalents:  well ordering, Zorn's lemma
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)
  ${
    $d x y z f g A $.  $d x y f B $.  $d x y f F $.  $d x y f G $.
    numthlem.1 $e |- A e. _V $.
    $( Hypotheses for transfinite recursion. $)
    numthlem.2 $e |- B = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.
    numthlem.3 $e |- F = U. B $.
    $( Generator function.  ` g ` is a choice function on ` ~P A ` . $)
    numthlem.4 $e |- G = { <. f , y >. | y = ( g ` ( A \ ran f ) ) } $.
    $( Lemma for ~ numth . $)
    numthlem $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( cv c0 wne cfv wcel wi cpw wral wex wf1o con0 wrex pwex ac4c cima cdif
      cres wa tfr2 crn wceq copab wfun cvv tfrlem7 visset resfunexg mp2an fvex
      rneq difeq2 fveq2 3syl fvopab fveq1i df-ima difeq2i fveq2i 3eqtr4i
      syl6eq eleq1d wss difss ssexi elpw mpbir neeq1 id eleq12d imbi12d rcla4v
      ax-mp imp syl5bir exp3a com12 r19.21aiv tfr1 tz7.49c mpan f1oeq1 cla4ev
      reximi 19.23aiv ) BMZNOZWQFMZPZWQQZRZBCSZTZFUAAMZCEMZUBZEUAZAUCUDZBXCFCIU
      EUFXDXIFXDCGXEUGZUHZNOZXEGPZXKQZRZAUCTZXECGXEUIZUBZAUCUDZXIXDXOAUCXEUCQZX
      DXOXTXDXLXNXTXNXKWSPZXKQZXDXLUJXTXMYAXKXTXMXQHPZYAABADEGHJKUKXQWQCXFULZUH
      ZWSPZUMEBUNZPCXQULZUHZWSPZYCYAEBXQYFYJGUOXEUPQXQUPQABDEGHJKUQAURGXEUPUSUT
      ZYIWSVAXFXQUMYDYHUMYEYIUMYFYJUMXFXQVBYDYHCVCYEYIWSVDVEVFXQHYGLVGXKYIWSXJY
      HCGXEVHVIVJVKVLVMXDXLYBXKXCQZXDXLYBRZRYLXKCVNCXJVOZXKCXKCIYNVPVQVRXBYMBXK
      XCWQXKUMZWRXLXAYBWQXKNVSYOWTYAWQXKWQXKWSVDYOVTWAWBWCWDWEWFWGWHWICUPQXPXSI
      ACUPGABDEGHJKWJWKWLXRXHAUCXGXREXQYKXECXFXQWMWNWOVEWPWD $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z w v u f g h A $.
    numth.1 $e |- A e. _V $.
    $( Numeration theorem: every set can be put into one-to-one correspondence
       with some ordinal (using AC).  Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth $p |- E. x e. On E. f f : x -1-1-onto-> A $=
      ( vy vg vz vw vu vv vh cv wfn cfv cres crn cdif wceq copab wral wa con0
      wrex cab cuni rdglem1 eqid id rneq difeq2 fveq2 3syl eqeqan12rd cbvopabv
      numthlem ) AEBFLZGLZMHLZUPNUPUROILZBJLZPZQZKLZNZRZJISZNRHUQTUAGUBUCFUDZCK
      VGUEZVFDGHAEFCVFUFVHUGVEELZBCLZPZQZVCNZRJICEUSVIRZUTVJRZUSVIVDVMVNUHVOVAV
      KRVBVLRVDVMRUTVJUIVAVKBUJVBVLVCUKULUMUNUO $.
      $( [10-Feb-1997] $)
  $}

  ${
    $d x y z A $.
    $( Numeration theorem: any set is equinumerous to some ordinal (using AC).
       Theorem 10.3 of [TakeutiZaring] p. 84. $)
    numth2 $p |- E. x e. On x ~~ A $=
      ( vy vz cvv wcel cv cen wbr con0 wrex wceq breq2 rexbidv wf1o wex visset
      numth bren rexbii mpbir vtoclg c0 wn breq1 rcla4ev 0elon enrefg ax-mp
      brprc mpbiri sylancr pm2.61i ) BEFZAGZBHIZAJKZUOCGZHIZAJKZUQCBEURBLUSUPAJ
      URBUOHMNUTUOURDGODPZAJKAURDCQZRUSVAAJUOURDVBSTUAUBUCJFZUCBHIZUQUNUDZUPVDA
      UCJUOUCBHUEUFUGVEVDUCUCHIZVCVFUGUCJUHUIUCBHUJUKULUM $.
      $( [20-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Any set is strictly dominated by some ordinal. $)
    numthcor $p |- ( A e. B -> E. x e. On A ~< x ) $=
      ( vy cv csdm wbr con0 wrex wceq breq1 rexbidv cpw cen numth2 cvv wcel wa
      wi visset sdomentr ax-mp canth2 pwex ensym sylancr reximi vtoclg ) DEZAEZ
      FGZAHIZBUJFGZAHIDBCUIBJUKUMAHUIBUJFKLUJUIMZNGZAHIULAUNOUOUKAHUIUNFGZUNUJN
      GZUKUOUJPQUPUQRUKSATUIUNUJPUAUBUIDTZUCUJUNUIURUDUEUFUGUBUH $.
      $( [22-Oct-2003] $)
  $}

  ${
    $d A f g y z $.  $d A h $.  $d A f x y z $.  $d w x y z $.
    $( Well-ordering theorem: any set ` A ` can be well-ordered.  This is an
       equivalent of the Axiom of Choice.  Theorem 6 of [Suppes] p. 242.  First
       proved by Ernst Zermelo (the "Z" in ZFC) in 1904.  (Contributed by Mario
       Carneiro, 5-Jan-2013.) $)
    weth $p |- ( A e. B -> E. x x We A ) $=
      ( vy vz vw cv wwe wex wceq weeq2 exbidv wal c0 wne cfv wcel wi wral ac4
      ax-gen aceq8 mpbir a4i vtoclg ) DGZAGZHZAIZBUGHZAIDBCUFBJUHUJAUFBUGKLUIDU
      IDMEGZNOUKFGPUKQREUFSFIZDMULDDEFTUADEFAUBUCUDUE $.
      $( [5-Jan-2013] $)
  $}

  ${
    $d x y z w f A $.
    wethOLD.1 $e |- A e. _V $.
    $( PLEASE PUT DESCRIPTION HERE. $)
    wethOLD $p |- E. x x We A $=
      ( vy vf vz vw cv wf1o wex con0 wrex wwe numth wcel wi ccnv f1ocnv cep
      cfv wbr copab eqid f1owe cxp cin weinxp xpex inex2 weeq1 cla4ev sylbi
      syl6 word eloni ordwe syl syl5 19.23aiv com12 r19.23aiv ax-mp ) DHZBEHZIZ
      EJZDKLBAHZMZAJZDBECNVFVIDKVFVCKOZVIVEVJVIPZEVEBVCVDQZIZVKVCBVDRVMVCSMZVIV
      JVMVNBFHVLTGHVLTSUAFGUBZMZVIFGBVCVOSVLVOUCUDVPBVOBBUEZUFZMZVIBVOUGVHVSAVR
      VQVOBBCCUHUIBVGVRUJUKULUMVJVCUNVNVCUOVCUPUQURUQUSUTVAVB $.
      $( [9-Mar-1997] $)
  $}

  ${
    $d x y w h t z f g u v r s a b A $.  $d h t f B $.
    $d x y z v u f g h t r s a b F $.  $d h t f G $.  $d t C $.
    $d y u v f t a b D $.  $d x y z w g u v f t s r a b R $.
    $d x y u v w f t s r a b $.
    zorn2lem.1 $e |- A e. _V $.
    $( Hypotheses for transfinite recursion. $)
    zorn2lem.2 $e |- B = { f | E. h e. On ( f Fn h /\
                A. t e. h ( f ` t ) = ( G ` ( f |` t ) ) ) } $.
    zorn2lem.3 $e |- F = U. B $.
    $( Auxiliary sets. $)
    zorn2lem.4 $e |- C = { z e. A | A. g e. ran f g R z } $.
    zorn2lem.5 $e |- D = { z e. A | A. g e. ( F " x ) g R z } $.
    $( Generator function.  ` w ` is a well-ordering on ` A ` . $)
    zorn2lem.6 $e |- G =
                { <. f , t >. | t = U. { v e. C | A. u e. C -. u w v } } $.
    $( Lemma for ~ zorn2 . $)
    zorn2lem1 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( F ` x ) e. D ) $=
      ( cv con0 wcel wwe c0 wne wa cfv wbr wn wral crab cuni cres tfr2 wceq
      copab fveq1i wfun cvv tfrlem7 visset resfunexg mp2an cima ssrab2 eqsstri
      ssexi rabex uniex cab crn rneq df-ima syl6eqr eleq2d imbi1d ralbidv2
      rabbidv 3eqtr4g anbi12d abbidv df-rab unieqd fvopab eqtri syl6eq eleq1d
      wss wereucl mp3an2 syl5bir imp ) AUCZUDUEZGCUCZUFZJUGUHZUIZWPOUJZJUEZWQXC
      EUCZDUCZWRUKULZEJUMZDJUNZUOZJUEZXAWQXBXIJWQXBOWPUPZPUJZXINFAHLOPRSUQXLXKF
      UCXFEIUMZDIUNZUOZURLFUSZUJXIXKPXPUBUTLFXKXOXIOVAWPVBUEXKVBUENFHLOPRSVCAVD
      OWPVBVEVFXHXGDJJGQJMUCZBUCKUKZMOWPVGZUMZBGUNZGUAXTBGVHVIZVJZVKVLLUCZXKURZ
      XNXHYEXEIUEZXMUIZDVMXEJUEZXGUIZDVMXNXHYEYGYIDYEYFYHXMXGYEIJXEYEXRMYDVNZUM
      ZBGUNYAIJYEYKXTBGYEXRXRMYJXSYEXQYJUEXQXSUEXRYEYJXSXQYEYJXKVNXSYDXKVOOWPVP
      VQVRVSVTWATUAWBZVRYEXFXFEIJYEXDIUEXDJUEXFYEIJXDYLVRVSVTWCWDXMDIWEXGDJWEWB
      WFWGWHWIWJWSJGWKWTXJYBDEGJWRYCWLWMWNWO $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem2 $p |- ( ( x e. On /\ ( w We A /\ D =/= (/) ) ) ->
                   ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
      ( cv con0 wcel wwe c0 wne wa cfv cima wbr wi wfun cdm wss funfvima2
      tfrlem7 onss wfn wceq tfr1 fndm ax-mp syl6ssr sylancr adantr wral
      zorn2lem1 breq2 ralbidv elrab2 simprbi breq1 rcla4cv 3syl syld ) AUDZUEUF
      ZHDUDUGKUHUIUJZUJZBUDZVSUFZWCPUKZPVSULZUFZWEVSPUKZLUMZVTWDWGUNZWAPUOVSPUP
      ZUQWJVTVSWCPUROGIMPQSTUSVTVSUEWKVSUTPUEVAWKUEVBOGIMPQSTVCUEPVDVEVFVGVHWBW
      HKUFZNUDZWHLUMZNWFVIZWGWIUNACDEFGHIJKLMNOPQRSTUAUBUCVJWLWHHUFWOWMCUDZLUMZ
      NWFVIWOCWHHKWPWHVBWQWNNWFWPWHWMLVKVLUBVMVNWNWINWEWFWMWEWHLVOVPVQVR $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem3 $p |- ( ( R Po A /\ ( x e. On /\ ( w We A /\ D =/= (/) ) ) ) ->
                   ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
      ( wpo cv con0 wcel wwe c0 wne wa cfv wbr wceq wn wi zorn2lem2 adantl
      breq1 biimprcd poirr nsyli com12 zorn2lem1 cima wral crab ssrab2 eqsstri
      sseli syl sylan2 syld ) HLUDZAUEZUFUGHDUEUHKUIUJUKUKZUKBUEZVOUGZVQPULZVOP
      ULZLUMZVTVSUNZUOZVPVRWAUPVNABCDEFGHIJKLMNOPQRSTUAUBUCUQURVNVTHUGZWAWCUPVP
      WAVNWDUKZWCWAWBVTVTLUMZWEWBWFWAVTVSVTLUSUTHVTLVAVBVCVPVTKUGWDACDEFGHIJKLM
      NOPQRSTUAUBUCVDKHVTKNUECUELUMNPVOVEVFZCHVGHUBWGCHVHVIVJVKVLVM $.
      $( [3-Apr-1997] $)

    $( Lemma for ~ zorn2 . $)
    zorn2lem4 $p |- ( ( R Po A /\ w We A ) -> E. x e. On D = (/) ) $=
      ( vy wpo cv wwe wa c0 wceq con0 wrex crn cvv wcel wn pm3.24 wne wi wal
      wss cfv ax-17 hba1 hban eleq1 zorn2lem1 wbr cima wral crab ssrab2
      eqsstri sseli syl syl5cbi exp32 com12 a2d a4sd imp r19.23ad wfn wb tfr1
      fvelrnb ax-mp syl5ib ssrdv ssex ex adantl ccnv wfun zorn2lem3 exp45
      com23 imp4a 19.21adv alimdv r2al syl6ibr cres ssid tz7.48lem mpan wrel
      cdm tfrlem6 fndm eqimssi relssres mp2an cnveqi funeq sylib syl6 onprc
      funrnex df-rn eleq1i dfdm4 eqtr3i 3imtr4g mtoi jcad df-ne ralbii df-ral
      ralnex 3bitr3i syl5ibr mt3i ) GKUDZGCUEUFZUGZJUHUIZAUJUKZOULZUMUNZYSUOZUG
      ZYSUPYOAUEZUJUNZJUHUQZURZAUSZUUAYQUOZYOUUFYSYTYNUUFYSURYMYNUUFYSYNUUFUGZY
      RGUTYSUUHUCYRGUUHUUBOVAZUCUEZUIZAUJUKZUUJGUNZUUJYRUNZUUHUUKUUMAUJYNUUFAYN
      AVBUUEAVCVDUUMAVBYNUUFUUCUUKUUMURZURZYNUUEUUPAYNUUCUUDUUOUUCYNUUDUUOURUUC
      YNUUDUUOUUKUUIGUNZUUMUUCYNUUDUGUGZUUIUUJGVEUURUUIJUNUUQABCDEFGHIJKLMNOPQR
      STUAUBVFJGUUIJMUEBUEKVGMOUUBVHVIZBGVJGUAUUSBGVKVLVMVNVOVPVQVRVSVTWAOUJWBZ
      UUNUULWCNFHLOPRSWDZAUJUUJOWEWFWGWHYRGQWIVNWJWKYOUUFOWLZWMZYTYOUUFUUIUUJOV
      AUIUOZUCUUBVIAUJVIZUVCYOUUFUUCUUJUUBUNZUGUVDURZUCUSZAUSUVEYOUUEUVHAYOUUEU
      VGUCYOUUEUUCUVFUVDYOUUCUUDUVFUVDURZYMYNUUCUUDUVIURZURYMUUCYNUVJYMUUCYNUUD
      UVIAUCBCDEFGHIJKLMNOPQRSTUAUBWNWOWPVTVRWQWRWSUVDAUCUJUUBWTXAUVEOUJXBZWLZW
      MZUVCUJUJUTUVEUVMUJXCAUCUJOUVAXDXEUVLUVBUIUVMUVCWCUVKOOXFOXGZUJUTUVKOUINF
      HLOPRSXHUVNUJUUTUVNUJUIUVAUJOXIWFZXJOUJXKXLXMUVLUVBXNWFXOXPUVCYSUJUMUNZXQ
      UVCUVBXGZUMUNZUVBULZUMUNZYSUVPUVRUVCUVTUMUVBXRVQYRUVQUMOXSXTUJUVSUMUVNUJU
      VSUVOOYAYBXTYCYDXPYEUUDAUJVIYPUOZAUJVIUUFUUGUUDUWAAUJJUHYFYGUUDAUJYHYPAUJ
      YIYJYKYL $.
      $( [3-Apr-1997] $)

    ${
      $d x u v f t s r a b H $.
      $( Auxiliary set.  ` H ` is ` D ` with ` x ` substituted for ` y ` . $)
      zorn2lem.7 $e |- H = { z e. A | A. g e. ( F " y ) g R z } $.
      $( Lemma for ~ zorn2 . $)
      zorn2lem5 $p |- ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) ->
            ( F " x ) C_ A ) $=
        ( vs cv wwe con0 wcel wa c0 wne wral cima cfv wceq wrex ax-17 hbra1
        hban wi wal eleq1 zorn2lem1 wbr crab ssrab2 eqsstri sseli syl syl5bi
        onelon sylani com12 exp43 com3r imp a2d a4sd df-ral syl5ib r19.23ad
        wfun tfrlem7 fvelima mpan syl5 ssrdv ) HDUGUHZAUGZUIUJZUKZRULUMZBWKUNZU
        KZUFPWKUOZHWPBUGZPUPZUFUGZUQZBWKURZWTHUJZWTWQUJZWPXAXCBWKWMWOBWMBUSWNBW
        KUTVAXCBUSWMWOWRWKUJZXAXCVBZVBZWMXEWNVBZBVCXGWOWMXHXGBWMXEWNXFWJWLXEWNX
        FVBZVBWLXEWJXIWLXEWJWNXFXAWLXEUKZWJWNUKZUKXCXAWRUIUJZXKXCXJXAWSHUJZXCXL
        XKUKZWSWTHVDXNWSRUJXMBCDEFGHIJRLMNOPQSTUAUBUEUDVERHWSRNUGCUGLVFNPWRUOUN
        ZCHVGHUEXOCHVHVIVJVKVLWKWRVMVNVOVPVQVRVSVTWNBWKWAWBVRWCPWDXDXBOGIMPQTUA
        WEBWTWKPWFWGWHWI $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn2 . $)
      zorn2lem6 $p |- ( R Po A ->
   ( ( ( w We A /\ x e. On ) /\ A. y e. x H =/= (/) ) -> R Or ( F " x ) ) ) $=
        ( vs vr vb va wpo cv wwe con0 wcel wa c0 wne wral cima wbr wceq w3o
        wor wss wi zorn2lem5 poss syl com12 wal cfv wex crab onelon anim12i
        anandis ex eqid zorn2lem2 adantll breq12 biimpcd syl6 com23 adantrrl
        imp eqeq12 fveq2 syl5bi adantl adantlr wb ancoms adantrrr 3orim123d
        word ordtri3or eloni syl2an syl5 exp31 com4r pm2.43i exp4a com3r a2d
        neeq1i ralbii imaeq2 raleqdv rabbidv neeq1d rcla4cv anim12d sylbi
        imp4b 19.23advv wfun wfn tfr1 fnfun ax-mp wrex fvelima df-rex sylib
        an4 2exbii eeanv bitri sylibr 19.21aivv r2al a1i jcad df-so syl6ibr )
        HLUJZHDUKULZAUKZUMUNZUOZRUPUQZBYTURZUOZPYTUSZLUJZUFUKZUGUKZLUTZUUHUUIVA
        ZUUIUUHLUTZVBZUGUUFURUFUUFURZUOUUFLVCYRUUEUUGUUNUUEYRUUGUUEUUFHVDYRUUGV
        EABCDEFGHIJKLMNOPQRSTUAUBUCUDUEVFUUFHLVGVHVIUUEUUNVEYRUUEUUHUUFUNZUUIUU
        FUNZUOZUUMVEZUGVJUFVJUUNUUEUURUFUGUUEUHUKZYTUNZUIUKZYTUNZUOZUUSPVKZUUHV
        AZUVAPVKZUUIVAZUOZUOZUIVLUHVLZUUMUUQUUEUVIUUMUHUIUUBUUDUVCUVHUUMUUBUVCN
        UKCUKLUTZNPUUSUSZURZCHVMZUPUQZUVKNPUVAUSZURZCHVMZUPUQZUOZVEZUVCUVHUUMVE
        ZVEUUDUUBUVCUVTUWBYSUUAUVCUVTUWBVEZVEUUAUVCYSUWCUUAUVCYSUVTUWBUUAUVCUUS
        UMUNZUVAUMUNZUOZYSUVTUOZUWBVEZUUAUVCUWFUUAUUTUVBUWFUUAUUTUOUWDUUAUVBUOU
        WEYTUUSVNYTUVAVNVOVPVQUWFUWHUWFUWGUVHUWFUUMUWFUWGUVHUWFUUMVEUWFUWGUOZUV
        HUOZUUSUVAUNZUUSUVAVAZUVAUUSUNZVBZUUMUWFUWJUWKUUJUWLUUKUWMUULUWIUVHUWKU
        UJVEZUWFYSUVSUVHUWOVEUVOUWFYSUVSUOZUOZUWKUVHUUJUWQUWKUVDUVFLUTZUVHUUJVE
        UWEUWPUWKUWRVEUWDUIUHCDEFGHIJUVRLMNOPQSTUAUBUVRVRUDVSVTUVHUWRUUJUVDUUHU
        VFUUILWAWBWCWDWEWFUVHUWLUUKVEUWIUVHUVDUVFVAUUKUWLUVDUUHUVFUUIWGUUSUVAPW
        HWIWJUWIUVHUWMUULVEZUWFYSUVOUVHUWSVEUVSUWFYSUVOUOZUOZUWMUVHUULUXAUWMUVF
        UVDLUTZUVHUULVEUWDUWTUWMUXBVEUWEUHUICDEFGHIJUVNLMNOPQSTUAUBUVNVRUDVSWKU
        VHUXBUULUVGUVEUXBUULWLUVFUUIUVDUUHLWAWMWBWCWDWNWFWOUUSWPUVAWPUWNUWDUWEU
        USUVAWQUUSWRUVAWRWSWTXAXBXCWCXDXEWFXFUUDUVKNPBUKZUSZURZCHVMZUPUQZBYTURZ
        UWAUUCUXGBYTRUXFUPUEXGXHUXHUUTUVOUVBUVSUXGUVOBUUSYTUXCUUSVAZUXFUVNUPUXI
        UXEUVMCHUXIUVKNUXDUVLUXCUUSPXIXJXKXLXMUXGUVSBUVAYTUXCUVAVAZUXFUVRUPUXJU
        XEUVQCHUXJUVKNUXDUVPUXCUVAPXIXJXKXLXMXNXOWTXPXQUUQUUTUVEUOZUHVLZUVBUVGU
        OZUIVLZUOZUVJPXRZUUQUXOVEPUMXSUXPOGIMPQTUAXTUMPYAYBUXPUUOUXLUUPUXNUXPUU
        OUXLUXPUUOUOUVEUHYTYCUXLUHUUHYTPYDUVEUHYTYEYFVQUXPUUPUXNUXPUUPUOUVGUIYT
        YCUXNUIUUIYTPYDUVGUIYTYEYFVQXNYBUVJUXKUXMUOZUIVLUHVLUXOUVIUXQUHUIUUTUVB
        UVEUVGYGYHUXKUXMUHUIYIYJYKWTYLUUMUFUGUUFUUFYMYKYNYOUFUGUUFLYPYQ $.
        $( [4-Apr-1997] $)

      $( Lemma for ~ zorn2 . $)
      zorn2lem7 $p |- ( ( R Po A /\
      A. s ( ( s C_ A /\ R Or s ) -> E. a e. A A. r e. s ( r R a \/ r = a ) ) )
                  -> E. a e. A A. b e. A -. a R b ) $=
        ( wpo cv wss wor wa wbr wceq wo wral wrex wi wal wwe wex wn cvv wcel
        weth ax-mp c0 con0 wne zorn2lem4 cima crab imaeq2 raleqdv rabbidv
        3eqtr4g eqeq1d onminex df-ne ralbii anbi2i rexbii sylibr zorn2lem5 a1i
        zorn2lem6 jcad wfun tfrlem7 visset funimaex sseq1 soeq2 anbi12d raleq
        rexbidv imbi12d cla4v sylan9 adantld imp noel sseld w3a potr 3anass
        sylan2br exp3a com23 breq1 biimprcd adantl jaod exp42 sylan9r com24
        imp31 a2d alimdv df-ral 3imtr4g wb eqeq1i eleq2 sylbi breq2 ralbidv
        elrab syl5bbr biimpd expdimp cbvralv syl5ib exp32 com34 mtoi exp4a ex
        com4r pm2.43a imp3a com4l 19.21adv syl6ibr imdistand reximdv2 com12
        adantr imp32 mpd exp45 imp4a com3l r19.23aiv 3syl adantlr pm2.43i
        19.23adv mpi ) HLUJZSUKZHULZUVCLUMZUNZTUKZUAUKZLUOZUVGUVHUPZUQZTUVCURZU
        AHUSZUTZSVAZUNZHDUKVBZDVCZUVHUBUKZLUOZVDZUBHURZUAHUSZHVEVFUVRUCDHVEVGVH
        UVPUVQUWCDUVPUVQUWCUVPUVQUNZUWCUVBUVQUWDUWCUTZUVOUVBUVQUNKVIUPZAVJUSZUW
        FRVIVKZBAUKZURZUNZAVJUSZUWEACDEFGHIJKLMNOPQUCUDUEUFUGUHVLUWGUWFRVIUPZVD
        ZBUWIURZUNZAVJUSUWLUWFUWMABUWIBUKZUPZKRVIUWRNUKZCUKZLUOZNPUWIVMZURZCHVN
        ZUXANPUWQVMZURZCHVNKRUWRUXCUXFCHUWRUXANUXBUXEUWIUWQPVOVPVQUGUIVRVSVTUWK
        UWPAVJUWJUWOUWFUWHUWNBUWIRVIWAWBWCWDWEUWKUWEAVJUWDUWIVJVFZUWKUWCUWDUXGU
        WFUWJUWCUVPUVQUXGUWFUWJUWCUTZUTUVPUWFUVQUXGUNZUXHUVPUWFUXIUWJUWCUVPUWFU
        XIUWJUNZUNZUNUVKTUXBURZUAHUSZUWCUVPUXKUXMUVPUXJUXMUWFUVBUXJUXBHULZUXBLU
        MZUNZUVOUXMUVBUXJUXNUXOUXJUXNUTUVBABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWFZWG
        ABCDEFGHIJKLMNOPQRUCUDUEUFUGUHUIWHWIUVNUXPUXMUTSUXBPWJUXBVEVFOGIMPQUDUE
        WKPUWIAWLWMVHUVCUXBUPZUVFUXPUVMUXMUXRUVDUXNUVEUXOUVCUXBHWNUVCUXBLWOWPUX
        RUVLUXLUAHUVKTUVCUXBWQWRWSWTXAXBXCUVPUWFUXJUXMUWCUTZUVBUWFUXJUXSUTZUTUV
        OUWFUVBUXTUWFUVBUXJUXSUWFUVBUXJUNZUNZUXLUWBUAHHUYBUVHHVFZUXLUWBUYBUYCUX
        LUWBUYBUYCUXLUNZUVSHVFZUWAUTZUBVAUWBUYBUYDUYFUBUYBUYCUXLUYFUYEUYBUYCUXL
        UWAUYEUWFUYAUYCUXLUWAUTZUTZUWFUYEUYAUYHUTUWFUYEUYAUYEUYHUWFUYEUYAUYEUYH
        UTUTUWFUYEUNZUYAUYCUYEUYGUYIUYAUYCUYEUYGUYIUYAUYCUYEUNZUXLUWAUYIUYAUYJU
        NZUNUXLUNUVTUVSVIVFZUVSXDUYIUYKUXLUVTUYLUTUYIUYKUVTUXLUYLUYIUYKUVTUXLUY
        LUTUYKUVTUNZUXLUVGUVSLUOZTUXBURZUYIUYLUYMUVGUXBVFZUVKUTZTVAUYPUYNUTZTVA
        UXLUYOUYMUYQUYRTUYMUYPUVKUYNUYAUYJUVTUYPUVKUYNUTZUTZUYAUVTUYJUYTUYAUYPU
        YJUVTUYSUXJUYPUVGHVFZUVBUYJUVTUYSUTUTUXJUXBHUVGUXQXEUVBVUAUYJUVTUYSUVBV
        UAUYJUNZUNZUVTUNUVIUYNUVJVUCUVTUVIUYNUTVUCUVIUVTUYNVUCUVIUVTUYNUVBVUAUY
        CUYEXFUVIUVTUNUYNUTVUBHUVGUVHUVSLXGVUAUYCUYEXHXIXJXKXCUVTUVJUYNUTVUCUVJ
        UYNUVTUVGUVHUVSLXLXMXNXOXPXQXRXKXSXTYAUVKTUXBYBUYNTUXBYBYCUYIUWSUVSLUOZ
        NUXBURZUYLUYOUWFUYEVUEUYLUWFUYEVUEUNZUYLUWFUVSUXDVFZUYLVUFUWFUXDVIUPVUG
        UYLYDKUXDVIUGYEUXDVIUVSYFYGUXCVUECUVSHUWTUVSUPUXAVUDNUXBUWTUVSUWSLYHYIY
        JYKYLYMUYNVUDTNUXBUVGUWSUVSLXLYNYOXQYPYQXSYRXPYSYQYTUUAUUBUUCUUDUUCUUEU
        WAUBHYBUUFXJUUGUUHYPUUIUUJUUKUULUUMXKYMUUNUUOUUPUUQUURUUSYTUUTUVA $.
        $( [6-Apr-1997] $)
    $}

  $}

  ${
    $d x y z $.  $d x y w $.
    $( Lemma for ~ zorn . $)
    zornlem $p |- ( z { <. x , y >. | x C. y } w <-> z C. w ) $=
      ( cv wpss copab visset psseq1 psseq2 eqid brab ) AEZBEZFZCEZNFPDEZFABPQOA
      BGZCHDHMPNINQPJRKL $.
      $( [13-Aug-2004] $)
  $}

  ${
    $d x y z w v u f g h t s r q a b c d j k m n R $.
    $d x y z w v u f g h t s r q a b c d j k m n A $.
    zorn2.1 $e |- A e. _V $.
    $( Zorn's Lemma of [Monk1] p. 117.  This theorem is equivalent to the Axiom
       of Choice and states that every partially ordered set ` A ` (with an
       ordering relation ` R ` ) in which every totally ordered subset has an
       upper bound, contains at least one maximal element.  The main proof
       consists of lemmas ~ zorn2lem1 through ~ zorn2lem7 ; this final piece
       mainly changes bound variables to eliminate the hypotheses of
       ~ zorn2lem7 . $)
    zorn2 $p |- ( ( R Po A /\
      A. w ( ( w C_ A /\ R Or w ) -> E. x e. A A. z e. w ( z R x \/ z = x ) ) )
                  -> E. x e. A A. y e. A -. x R y ) $=
      ( vt vu vr vq vn vj vg va vb vc vk vm vv vh vd vs vf cv wfn cfv cres wbr
      wn crn wral crab cuni wceq copab wa con0 wrex cab cima rdglem1 eqid
      breq2 ralbidv breq1 cbvralv syl5bb cbvrabv id wcel rneq raleqdv rabbidv
      eleq2d wb raleq notbid syl anbi12d abbidv eleq1 cbvabv syl5eq df-rab
      3eqtr4g unieqd eqeqan12rd cbvopabv zorn2lem7 ) HIJKLMNEOUEZPUEZUFQUEZWKUG
      WKWMUHRUEZWNSUEZKUEZUIZUJZRWPTUEZFUIZKUAUEZUKZULZTEUMZULZSXDUMZUNZUOZUARU
      PZUGUOQWLULUQPURUSOUTZWTKUBUEZUKZULZTEUMZUCUEZJUEZFUIZUCXJUNZHUEVAULJEUMZ
      FUBUCUDXRXIXQUCXRIUEVAULJEUMZDCABGPQUDNOUBXIVBXRVCXMXQUCXLULZTJEWSXPUOZXO
      WSFUIZUCXLULYAXMYBYCXQUCXLWSXPXOFVDVEWTYCKUCXLWPXOWSFVFVGVHVIXSVCXHNUEZMU
      EZLUEZWPUIZUJZMXNULZLXNUMZUNZUOUARUBNWNYDUOZXAXKUOZWNYDXGYKYLVJYMXFYJYMWO
      XDVKZXEUQZSUTZYFXNVKZYIUQZLUTZXFYJYMYFXDVKZWNYFWPUIZUJZRXDULZUQZLUTYSYPYM
      UUDYRLYMYTYQUUCYIYMXDXNYFYMXCXMTEYMWTKXBXLXAXKVLVMVNZVOYMXDXNUOZUUCYIVPUU
      EUUFYHMXDULYIUUCYHMXDXNVQUUBYHRMXDWNYEUOUUAYGWNYEYFWPVFVRVGVHVSVTWAYOUUDS
      LWOYFUOZYNYTXEUUCWOYFXDWBUUGWRUUBRXDUUGWQUUAWOYFWNWPVDVRVEVTWCWDXESXDWEYI
      LXNWEWFWGWHWIXTVCWJ $.
      $( [6-Apr-1997] $)

    $( Zorn's Lemma.  If the union of every chain (with respect to inclusion)
       in a set belongs to the set, then the set contains a maximal element.
       This theorem is equivalent to the Axiom of Choice.  Theorem 6M of
       [Enderton] p. 151.  See ~ zorn2 for a version with general partial
       orderings. $)
    zorn $p |- ( A. z ( ( z C_ A /\
                  A. x e. z A. y e. z ( x C_ y \/ y C_ x ) ) -> U. z e. A ) ->
                E. x e. A A. y e. A -. x C. y ) $=
      ( vw vv vu cv wss wo wral wa cuni wcel wi wal wpss copab wbr wn wrex wpo
      wor wceq zorn2 w3a pssirr zornlem mtbir psstr sylibr syl2anb pm3.2i a1i
      rgen3 df-po mpbir w3o df-so simprbi biid 3orbi123i sspsstri bitr4i
      2ralbii sylib anim2i risset eqimss2 unissb orbi1i sspss ralbii reximi
      sylbi imim12i alimi sylancr notbii rexbii ) CIZDJZAIZBIZJWEWDJKZBWBLAWBLZ
      MZWBNZDOZPZCQZWDWEFIGIRFGSZTZUAZBDLZADUBZWDWERZUAZBDLZADUBDWMUCZWCWBWMUDZ
      MZHIZWDWMTZXDWDUEZKZHWBLZADUBZPZCQWQWLABHCDWMEUFXAXDXDWMTZUAZXDWEWMTZWEWD
      WMTZMXEPZMZADLBDLHDLXPHBADDDXPXDDOWEDOWDDOUGXLXOXKXDXDRXDUHFGHHUIUJXDWERZ
      WEWDRZXEXMXNXQXRMXDWDRZXEXDWEWDUKFGHAUIZULFGHBUIFGBAUIZUMUNUOUPHBADWMUQUR
      WKXJCXCWHWJXIXBWGWCXBWNWDWEUEZXNUSZBWBLAWBLZWGXBWBWMUCYDABWBWMUTVAYCWFABW
      BWBYCWRYBXRUSWFWNWRYBYBXNXRFGABUIZYBVBYAVCWDWEVDVEVFVGVHWJWDWIUEZADUBXIAW
      IDVIYFXHADYFXDWDJZHWBLZXHYFWIWDJYHWIWDVJHWBWDVKVGXGYGHWBXGXSXFKYGXEXSXFXT
      VLXDWDVMVEVNULVOVPVQVRVSWPWTADWOWSBDWNWRYEVTVNWAVG $.
      $( [12-Aug-2004] $)
  $}

  ${
    $d f F $.  $d f A $.  $d f B $.
    fodom.1 $e |- A e. _V $.
    $( An onto function implies dominance of domain over range.  Lemma 10.20 of
       [Kunen] p. 30.  This theorem uses the Axiom of Choice ~ ac7g .  AC is
       not needed for finite sets - see ~ fodomfi . $)
    fodom $p |- ( F : A -onto-> B -> B ~<_ A ) $=
      ( vf wfo cv ccnv wss cdm wfn wa wex cdom wbr cvv wcel wf fex fof sylancl
      cnvexg ac7g 3syl wi crn forn df-rn syl5eqr fneq2d wf1 wfun df-f1 dffn3
      biimpi ad2antlr funss impcom fofun sstr cnvss cnvcnvss syl2an adantlr
      sylanbrc visset rnex f1dom2g ax-mp syl rnss adantl wceq fdm dfdm4 adantr
      sseqtrd ssdomg domtr syl11anc exp31 sylbid com23 imp3a 19.23adv mpd ) ABC
      FZEGZCHZIZWHWIJZKZLZEMZBANOZWGCPQZWIPQWNABCRZAPQWPWGABPCSABCTZDUACPUBPWIE
      UCUDWGWMWOEWGWJWLWOWGWLWJWOWGWLWHBKZWJWOUEWGWKBWHWGCUFBWKABCUGCUHUIUJWGWS
      WJWOWGWSLWJLZBWHUFZNOZXAANOZWOWTBXAWHUKZXBXDBXAWHRZWHHZULZWTBXAWHUMWSXEWG
      WJWSXEBWHUNUOUPWGWJXGWSCULZXFCIZXGWGWJXIXHXGXFCUQURABCUSXFWIHZIXJCIXIWJXF
      XJCUTWHWIVACVBUAVCVDVEXAPQZXDXBUEWHEVFVGZBXAPWHVHVIVJWGWJXCWSWGWJLZXAAIZX
      CXMXAWIUFZAWJXAXOIWGWHWIVKVLWGXOAVMWJWGCJZAXOWGWQXPAVMWRABCVNVJCVOUIVPVQX
      KXNXCUEXLXAAPVRVIVJVDBXAAVSVTWAWBWCWDWEWF $.
      $( [23-Jul-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.
    $( An onto function implies dominance of domain over range. $)
    fodomg $p |- ( A e. C -> ( F : A -onto-> B -> B ~<_ A ) ) $=
      ( vx cv wfo cdom wbr wi wceq foeq2 breq2 imbi12d visset fodom vtoclg ) EF
      ZBDGZBRHIZJABDGZBAHIZJEACRAKSUATUBRABDLRABHMNRBDEOPQ $.
      $( [23-Jul-2004] $)
  $}

  ${
    $d f g z A $.  $d f g z B $.
    fodomb.1 $e |- A e. _V $.
    $( Equivalence of an onto mapping and dominance for a non-empty set.
       Proposition 10.35 of [TakeutiZaring] p. 93. $)
    fodomb $p |- ( ( A =/= (/) /\ E. f f : A -onto-> B ) <->
                 ( (/) ~< B /\ B ~<_ A ) ) $=
      ( c0 wne cv wfo wex wa csdm wbr cdom cdm wceq wf fof fdm syl eqeq1d crn
      forn dm0rn0 syl5bb bitr3d necon3bid biimpac wb cvv wcel wi fornex ax-mp
      0sdomg adantl mpbird ex fodom a1i jcad 19.23adv imp sdomdomtr 0sdom
      sylib fodomr mp3an1 jca impbii ) AEFZABCGZHZCIZJEBKLZBAMLZJZVJVMVPVJVLVPC
      VJVLVNVOVJVLVNVJVLJVNBEFZVLVJVQVLAEBEVLVKNZEOZAEOBEOZVLVRAEVLABVKPVRAOABV
      KQABVKRSTVLVKUAZEOVTVSVLWABEABVKUBTVKUCUDUEUFUGVLVNVQUHZVJVLBUIUJZWBAUIUJ
      ZVLWCUKDABUIVKULUMBUIUNSUOUPUQVLVOUKVJABVKDURUSUTVAVBVPVJVMVPEAKLZVJWDVPW
      EUKDEBAUIVCUMADVDVEWDVNVOVMDABUICVFVGVHVI $.
      $( [24-Feb-2007] $) $( [29-Jul-2004] $)
  $}

  ${
    $d f x y A $.  $d f x y B $.
    brdom3.1 $e |- A e. _V $.
    brdom3.2 $e |- B e. _V $.
    $( Equivalence to a dominance relation. $)
    brdom3 $p |- ( A ~<_ B <-> E. f ( A. x E* y x f y /\
                 A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wal wrex wral wa wex c0 wceq wfo wo wn wi csdm cvv
      wcel fodomr mp3an1 wne 0sdom df-ne bitr2i sylanb ancoms pm5.6 mpbi 0ex
      ax-17 breq mobid albidv rexbidv ralbidv anbi12d cla4ev cop noel df-br
      mtbir nex exmo ori ax-mp ax-gen rzal sylancr wfun fofun wrel dffun6
      simprbi syl wf dffo4 jca eximi jaoi cxp cin cdm wfn crn inss1 ssbri
      immoi alimi relxp relin2 mpbiran sylibr funfn sylib rninxp biimpri
      anim12i df-fo visset inex1 dmex fodom wss inss2 dmss dmxpss sstri
      ssdom2g mp2 domtr mpan2 3syl 19.23aiv impbii ) CDHIZAJZBJZEJZIZBKZALZYDYC
      YEIZBDMZACNZOZEPZYBCQRZDCYESZEPZTZYMYBYNUAZOYPUBYBYQUBYRYBYPQCUCIZYBYPYRD
      UDUEZYSYBYPGDCUDEUFUGYSCQUHYRCFUICQUJUKULUMYBYNYPUNUOYNYMYPYCYDQIZBKZALZY
      DYCQIZBDMZACNZYMYNYLUUCUUFOEQUPYEQRZYHUUCYKUUFUUGYGUUBAUUGYFUUABUUGBUQYCY
      DYEQURUSUTUUGYJUUEACUUGYIUUDBDYDYCYEQURVAVBVCVDUUBAUUABPZUAUUBUUABUUAYCYD
      VEZQUEUUIVFYCYDQVGVHVIUUHUUBUUABVJVKVLVMUUEACVNVOYOYLEYOYHYKYOYEVPZYHDCYE
      VQUUJYEVRYHABYEVSVTWAYODCYEWBYKBADCYEWCVTWDWEWFWAYLYBEYLYEDCWGZWHZWIZCUUL
      SZCUUMHIZYBYLUULUUMWJZUULWKCRZOUUNYHUUPYKUUQYHUULVPZUUPYHYCYDUULIZBKZALZU
      URYGUUTAUUSYFBUULYEYCYDYEUUKWLWMWNWOUURUULVRZUVAABUULVSUUKVRUVBDCWPYEUUKW
      QVLWRWSUULWTXAUUQYKBADCYEXBXCXDUUMCUULXEWSUUMCUULUULYEUUKEXFXGXHXIUUOUUMD
      HIZYBYTUUMDXJUVCGUUMUUKWIZDUULUUKXJUUMUVDXJYEUUKXKUULUUKXLVLDCXMXNUUMDUDX
      OXPCUUMDXQXRXSXTYA $.
      $( [27-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom4.1 $e |- A e. _V $.
    brdom4.2 $e |- B e. _V $.
    $( An equivalence to a dominance relation. $)
    brdom5 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y x f y /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wmo wral wrex wa wex wal brdom3 alral anim1i eximi sylbi
      cxp cin cdm wfo wfn crn wceq wfun wrel wcel wss inss2 dmss ax-mp dmxpss
      sstri sseli inss1 ssbri immoi imim12i ralimi2 relxp relin2 jctil dffun7
      sylibr funfn sylib rninxp biimpri anim12i df-fo visset inex1 dmex fodom
      cvv ssdom2g mp2 domtr mpan2 3syl 19.23aiv impbii ) CDHIZAJZBJZEJZIZBKZADL
      ZWIWHWJIBDMACLZNZEOZWGWLAPZWNNZEOWPABCDEFGQWRWOEWQWMWNWLADRSTUAWOWGEWOWJD
      CUBZUCZUDZCWTUEZCXAHIZWGWOWTXAUFZWTUGCUHZNXBWMXDWNXEWMWTUIZXDWMWTUJZWHWIW
      TIZBKZAXALZNXFWMXJXGWLXIADXAWHXAUKWHDUKWLXIXADWHXAWSUDZDWTWSULXAXKULWJWSU
      MWTWSUNUODCUPUQZURXHWKBWTWJWHWIWJWSUSUTVAVBVCWSUJXGDCVDWJWSVEUOVFABWTVGVH
      WTVIVJXEWNBADCWJVKVLVMXACWTVNVHXACWTWTWJWSEVOVPVQVRXCXADHIZWGDVSUKXADULXM
      GXLXADVSVTWACXADWBWCWDWEWF $.
      $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation. $)
    brdom4 $p |- ( A ~<_ B <->
  E. f ( A. x e. B E* y ( y e. A /\ x f y ) /\ A. x e. A E. y e. B y f x ) ) $=
      ( cdom wbr cv wcel wa wmo wral wrex wex wal brdom3 moan alimi alral syl
      anim1i eximi sylbi cxp cin cdm wfo wfn crn wceq wfun wrel wss inss2 dmss
      ax-mp dmxpss sstri sseli rnss rnxpss inss1 ssbri anim12i immoi imim12i
      ralimi2 relxp relin2 jctil dffun9 sylibr funfn sylib rninxp biimpri
      df-fo visset inex1 dmex fodom cvv ssdom2g mp2 domtr mpan2 3syl 19.23aiv
      impbii ) CDHIZBJZCKZAJZWMEJZIZLZBMZADNZWMWOWPIBDOACNZLZEPZWLWQBMZAQZXALZE
      PXCABCDEFGRXFXBEXEWTXAXEWSAQWTXDWSAWQWNBSTWSADUAUBUCUDUEXBWLEXBWPDCUFZUGZ
      UHZCXHUIZCXIHIZWLXBXHXIUJZXHUKZCULZLXJWTXLXAXNWTXHUMZXLWTXHUNZWMXMKZWOWMX
      HIZLZBMZAXINZLXOWTYAXPWSXTADXIWOXIKWODKWSXTXIDWOXIXGUHZDXHXGUOZXIYBUOWPXG
      UPZXHXGUQURDCUSUTZVAXSWRBXQWNXRWQXMCWMXMXGUKZCYCXMYFUOYDXHXGVBURDCVCUTVAX
      HWPWOWMWPXGVDVEVFVGVHVIXGUNXPDCVJWPXGVKURVLABXHVMVNXHVOVPXNXABADCWPVQVRVF
      XICXHVSVNXICXHXHWPXGEVTWAWBWCXKXIDHIZWLDWDKXIDUOYGGYEXIDWDWEWFCXIDWGWHWIW
      JWK $.
      $( [28-Mar-2007] $)
  $}

  ${
    $d f g x y A v w z $.  $d f g x y B v w z $.
    brdom7disj.1 $e |- A e. _V $.
    brdom7disj.2 $e |- B e. _V $.
    brdom7disj.3 $e |- ( A i^i B ) = (/) $.
    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom7disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      ( y e. A /\ { x , y } e. f ) /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vg vv vz vw cdom wbr cv wcel wa wmo wral wrex wex cpr brdom4 wceq cop
      cab csn snex simpl ss2abi df-sn sseqtr4i ssexi abrexex2 ax-17 eleq2
      anbi2d mobid ralbidv rexbidv anbi12d cla4ev wb wne cin c0 incom eqtri
      disjne mp3an1 visset opthpr syl equcom anbi12i syl6rbb df-br a1i
      rexbidva rexcom zfpair2 eqeq1 anbi1d 2rexbidv elab bitr4i adantr breq1
      breq2 ceqsrex2v bitrd pm5.32da ralbiia ancoms eqcom ancom 3bitr4g bicomi
      syl5bb syl2anbr 19.23aiv copab cvv df-opab cuni uniex prid1 elunii mpan
      adantl prid2 eqeltrri abexex eqeltri breq preq1 eleq1d preq2 eqid brab
      anbi2i mobii ralbii rexbii impbii bitri ) CDMNBOZCPZAOZYGIOZNZQZBRZADSZYG
      YIYJNZBDTZACSZQZIUAZYHYIYGUBZEOZPZQZBRZADSZYGYIUBZUUAPZBDTZACSZQZEUAZABCD
      IFGUCYSUUKYRUUKIYHYTJOZKOZLOZUBZUDZUUMUUNUEYJPZQZKDTZLCTZJUFZPZQZBRZADSZU
      UFUVAPZBDTZACSZUUKYNYQUUJUVEUVHQEUVAUUSLJCFUURKJDGUURJUFZUUOUGZUUOUHUVIUU
      PJUFUVJUURUUPJUUPUUQUIUJJUUOUKULUMUNUNUUAUVAUDZUUEUVEUUIUVHUVKUUDUVDADUVK
      UUCUVCBUVKBUOUVKUUBUVBYHUUAUVAYTUPUQURUSUVKUUHUVGACUVKUUGUVFBDUUAUVAUUFUP
      UTUSVAVBUVDYMADYIDPZUVCYLBUVLBUOUVLYHUVBYKUVLYHQUVBUUMYIUDZUUNYGUDZQZUUMU
      UNYJNZQZLCTZKDTZYKUVLUVBUVSVCYHUVLUVSYTUUOUDZUUQQZLCTZKDTZUVBUVLUVRUWBKDU
      VLUVQUWALCUVLUUNCPZQZUVOUVTUVPUUQUWEUVTYIUUMUDZYGUUNUDZQZUVOUWEYIUUNVDZUV
      TUWHVCDCVEZVFUDZUVLUWDUWIUWJCDVEVFDCVGHVHZDCYIUUNVIVJYIYGUUMUUNAVKZBVKZKV
      KZLVKZVLVMUWFUVMUWGUVNAKVNBLVNVOVPUVPUUQVCUWEUUMUUNYJVQZVRVAVSUTUWCUWAKDT
      LCTZUVBUWAKLDCVTUUTUWRJYTABWAUULYTUDZUURUWALKCDUWSUUPUVTUUQUULYTUUOWBWCWD
      WEWFVPWGUVPYIUUNYJNYKKLYIYGDCUUMYIUUNYJWHUUNYGYIYJWIWJWKWLURWMUVGYPACYICP
      ZUVFYOBDUWTYGDPZQUVFUUNYIUDZUUMYGUDZQZUVPQZKDTZLCTZYOUWTUVFUXGVCUXAUWTUUF
      UUOUDZUUQQZKDTZLCTZUXGUVFUWTUXJUXFLCUWTUXIUXEKDUWTUUMDPZQZUXHUXDUUQUVPUXM
      UUOUUFUDZUXCUXBQZUXHUXDUXMUUMYIVDZUXNUXOVCUXLUWTUXPUWKUXLUWTUXPUWLDCUUMYI
      VIVJWNUUMUUNYGYIUWOUWPUWNUWMVLVMUUFUUOWOUXBUXCWPWQUUQUVPVCUXMUVPUUQUWQWRV
      RVAVSUTUUTUXKJUUFBAWAUULUUFUDZUURUXILKCDUXQUUPUXHUUQUULUUFUUOWBWCWDWEWSWG
      UVPUUMYIYJNYOLKYIYGCDUUNYIUUMYJWIUUMYGYIYJWHWJWKVSWMWTXAUUJYSEYHYIYGUUNUU
      MUBZUUAPZLKXBZNZQZBRZADSZYGYIUXTNZBDTZACSZYSUUEUUIYRUYDUYGQIUXTUXTUULUUNU
      UMUEZUDZUXSQZKUAZLUAJUFXCUXSLKJXDUYKLJUUAXEZUUAEVKXFZUYJUUNUYLPZKUXSUYNUY
      IUUNUXRPUXSUYNUUNUUMUWPXGUUNUXRUUAXHXIXJXAUYJKJUYLUYMUXSUUMUYLPZUYIUUMUXR
      PUXSUYOUUNUUMUWOXKUUMUXRUUAXHXIXJUYJJUFUYIJUFZUYHUGUYPXCJUYHUKUYHUHXLUYJU
      YIJUYIUXSUIUJUMXMXMXNYJUXTUDZYNUYDYQUYGUYQYMUYCADUYQYLUYBBUYQBUOUYQYKUYAY
      HYIYGYJUXTXOUQURUSUYQYPUYFACUYQYOUYEBDYGYIYJUXTXOUTUSVAVBUYCUUDADUYBUUCBU
      YAUUBYHUXSYIUUMUBZUUAPUUBLKYIYGUXTUWMUWNUXBUXRUYRUUAUUNYIUUMXPXQUXCUYRYTU
      UAUUMYGYIXRXQUXTXSZXTYAYBYCUYFUUHACUYEUUGBDUXSYGUUMUBZUUAPUUGLKYGYIUXTUWN
      UWMUVNUXRUYTUUAUUNYGUUMXPXQUVMUYTUUFUUAUUMYIYGXRXQUYSXTYDYCWTXAYEYF $.
      $( [29-Mar-2007] $)

    $( An equivalence to a dominance relation for disjoint sets. $)
    brdom6disj $p |- ( A ~<_ B <-> E. f ( A. x e. B E* y
      { x , y } e. f /\ A. x e. A E. y e. B { y , x } e. f ) ) $=
      ( vg vv vz vw cdom wbr cv wmo wral wrex wa wex cpr wcel brdom5 wceq cop
      cab csn snex simpl ss2abi df-sn sseqtr4i ssexi abrexex2 ax-17 eleq2
      mobid ralbidv rexbidv anbi12d cla4ev wi wal wne wb cin c0 incom eqtri
      disjne mp3an1 visset opthpr syl breq12 biimprd syl6bi imp3a ex adantrd
      r19.23advv zfpair2 eqeq1 anbi1d df-br anbi2i syl6bbr 2rexbidv elab
      syl5ib 19.21aiv immo ralimia ancoms eqcom ancom 3bitr4g bicomi a1i
      rexbidva syl5bb adantr breq2 breq1 ceqsrex2v bitrd ralbiia biimpri
      syl2an 19.23aiv copab cvv df-opab cuni uniex prid1 elunii mpan adantl
      prid2 eqeltrri abexex eqeltri breq preq1 eleq1d preq2 eqid brab mobii
      ralbii rexbii syl2anbr impbii bitri ) CDMNAOZBOZIOZNZBPZADQZYQYPYRNZBDRZA
      CQZSZITZYPYQUAZEOZUBZBPZADQZYQYPUAZUUHUBZBDRZACQZSZETZABCDIFGUCUUFUUQUUEU
      UQIUUGJOZKOZLOZUAZUDZUUSUUTUEYRUBZSZKDRZLCRZJUFZUBZBPZADQZUULUVGUBZBDRZAC
      QZUUQUUAUUDUUPUVJUVMSEUVGUVELJCFUVDKJDGUVDJUFZUVAUGZUVAUHUVNUVBJUFUVOUVDU
      VBJUVBUVCUIUJJUVAUKULUMUNUNUUHUVGUDZUUKUVJUUOUVMUVPUUJUVIADUVPUUIUVHBUVPB
      UOUUHUVGUUGUPUQURUVPUUNUVLACUVPUUMUVKBDUUHUVGUULUPUSURUTVAYTUVIADYPDUBZUV
      HYSVBZBVCYTUVIVBUVQUVRBUVQUUGUVAUDZUUSUUTYRNZSZKDRLCRZYSUVHUVQUWAYSLKCDUV
      QUUTCUBZUWAYSVBZUUSDUBZUVQUWCUWDUVQUWCSZUVSUVTYSUWFUVSYPUUSUDYQUUTUDSZUVT
      YSVBUWFYPUUTVDZUVSUWGVEDCVFZVGUDZUVQUWCUWHUWICDVFVGDCVHHVIZDCYPUUTVJVKYPY
      QUUSUUTAVLZBVLZKVLZLVLZVMVNUWGYSUVTYPUUSYQUUTYRVOVPVQVRVSVTWAUVFUWBJUUGAB
      WBUURUUGUDZUVDUWALKCDUWPUVDUVSUVCSUWAUWPUVBUVSUVCUURUUGUVAWCWDUVTUVCUVSUU
      SUUTYRWEZWFWGWHWIWJWKUVHYSBWLVNWMUVMUUDUVLUUCACYPCUBZUVKUUBBDUWRYQDUBZSUV
      KUUTYPUDZUUSYQUDZSZUVTSZKDRZLCRZUUBUWRUVKUXEVEUWSUWRUULUVAUDZUVCSZKDRZLCR
      ZUXEUVKUWRUXHUXDLCUWRUXGUXCKDUWRUWESZUXFUXBUVCUVTUXJUVAUULUDZUXAUWTSZUXFU
      XBUXJUUSYPVDZUXKUXLVEUWEUWRUXMUWJUWEUWRUXMUWKDCUUSYPVJVKWNUUSUUTYQYPUWNUW
      OUWMUWLVMVNUULUVAWOUWTUXAWPWQUVCUVTVEUXJUVTUVCUWQWRWSUTWTUSUVFUXIJUULBAWB
      UURUULUDZUVDUXGLKCDUXNUVBUXFUVCUURUULUVAWCWDWHWIXAXBUVTUUSYPYRNUUBLKYPYQC
      DUUTYPUUSYRXCUUSYQYPYRXDXEXFWTXGXHXIXJUUPUUFEYPYQUUTUUSUAZUUHUBZLKXKZNZBP
      ZADQZYQYPUXQNZBDRZACQZUUFUUKUUOUUEUXTUYCSIUXQUXQUURUUTUUSUEZUDZUXPSZKTZLT
      JUFXLUXPLKJXMUYGLJUUHXNZUUHEVLXOZUYFUUTUYHUBZKUXPUYJUYEUUTUXOUBUXPUYJUUTU
      USUWOXPUUTUXOUUHXQXRXSXJUYFKJUYHUYIUXPUUSUYHUBZUYEUUSUXOUBUXPUYKUUTUUSUWN
      XTUUSUXOUUHXQXRXSUYFJUFUYEJUFZUYDUGUYLXLJUYDUKUYDUHYAUYFUYEJUYEUXPUIUJUMY
      BYBYCYRUXQUDZUUAUXTUUDUYCUYMYTUXSADUYMYSUXRBUYMBUOYPYQYRUXQYDUQURUYMUUCUY
      BACUYMUUBUYABDYQYPYRUXQYDUSURUTVAUXSUUJADUXRUUIBUXPYPUUSUAZUUHUBUUILKYPYQ
      UXQUWLUWMUWTUXOUYNUUHUUTYPUUSYEYFUXAUYNUUGUUHUUSYQYPYGYFUXQYHZYIYJYKUYBUU
      NACUYAUUMBDUXPYQUUSUAZUUHUBUUMLKYQYPUXQUWMUWLUUTYQUDUXOUYPUUHUUTYQUUSYEYF
      UUSYPUDUYPUULUUHUUSYPYQYGYFUYOYIYLYKYMXJYNYO $.
      $( [5-Apr-2007] $)
  $}

  $( An image of a function under a set is dominated by the set.  Proposition
     10.34 of [TakeutiZaring] p. 92. $)
  imadomg $p |- ( A e. B -> ( Fun F -> ( F " A ) ~<_ A ) ) $=
    ( wcel wfun cima cres cdm cdom wbr wa crn cvv wfo resfunexg dmexg syl
    funres funforn sylib adantr fodomg sylc df-ima syl5eqbr expcom domtr wss
    cin dmres inss1 eqsstri ssdom2g mpi sylan2 syld ) ABDZCEZCAFZCAGZHZIJZUSAIJ
    ZURUQVBURUQKZUTLZVAUSIVDVAMDZVAVEUTNZVEVAIJVDUTMDVFCABOUTMPQURVGUQURUTEVGAC
    RUTSTUAVAVEMUTUBUCCAUDUEUFVBUQVCVBVAAIJZVCUQUSVAAUGUQVAAUHVHVAACHZUIACAUJAV
    IUKULVAABUMUNUOUFUP $.
    $( [23-Jul-2004] $)

  $( The range of a function is dominated by its domain. $)
  fnrndomg $p |- ( A e. B -> ( F Fn A -> ran F ~<_ A ) ) $=
    ( wcel crn wfo cdom wbr wfn fodomg dffn4 syl5ib ) ABDACEZCFMAGHCAIAMBCJACKL
    $.
    $( [1-Sep-2004] $)

  ${
    $d f g h v w x y z A $.  $d f g h v w x y z B $.
    unidom.1 $e |- A e. _V $.
    unidom.2 $e |- B e. _V $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98. $)
    unidom $p |- ( A. x e. A x ~<_ B -> U. A ~<_ ( A X. B ) ) $=
      ( vf vh vg vy vz cv cdom wbr wral cfv wf1 wex wcel wa cuni cxp brdom
      ralbii f1eq1 ac6s3 sylbi uniex wceq eleq2 eleq1 anbi12d eluni biimpi
      mprg a1i wi cvv cop id fveq2 eleq12d eleq1d rcla4cv simpr syl6 adantl wf
      wb syl f1eq2 bitrd rcla4v f1f ffvelrn expcom sylan9r com3r imp jcad
      opelxpi fveq1d eqeq2d imp31 adantrr adantr simpl biimprd sylan9 anim12d
      an32s adantlll f1fveq biimpd syl11anc sylbird expimpd fvex opth syl5ib
      fveq2d eqtrd opeq12d impbid1 ex dom2d mpi 19.23adv 19.23aiv sylc ) AKZCLM
      ZABNZXJCXJFKZOZPZABNZFQZXJXJGKZOZRZXSBRZSZABTZNZGQZYCBCUAZLMZXLXJCHKZPZHQ
      ZABNXQXKYJABXJCHEUBUCYIXOAHBFDXJCYHXNUDUEUFYEXLXJYHRZYHBRZSZHQZYEAYCYMYBA
      HYCGBDUGZYHXSUHYKXTYLYAYHXSXJUIYHXSBUJUKUEXJYCRYNHXJBULUMUNUOXPYEYGUPFXPY
      DYGGXPYDYGXPYDSZYCUQRYGYOYPIJYCYFIKZXROZYQYRXMOZOZURZJKZXROZUUBUUCXMOZOZU
      RZUQYPYQYCRZYRBRZYTCRZSUUAYFRYPUUGUUHUUIYDUUGUUHUPXPYDUUGYQYRRZUUHSZUUHYB
      UUKAYQYCXJYQUHZXTUUJYAUUHUULXJYQXSYRUULUSXJYQXRUTZVAUULXSYRBUUMVBUKVCZUUJ
      UUHVDVEVFXPYDUUGUUIUPYDUUGXPUUIYDUUGUUKXPUUIUPUUNUUHXPYRCYSVGZUUJUUIUUHXP
      YRCYSPZUUOXOUUPAYRBXJYRUHZXOXJCYSPZUUPUUQXNYSUHXOUURVHXJYRXMUTXJCXNYSUDVI
      XJYRCYSVJVKVLZYRCYSVMVEUUOUUJUUIYRCYQYSVNVOVPVEVQVRVSYRYTBCVTVEYPUUGUUBYC
      RZSZUUAUUFUHZYQUUBUHZVHYPUVASZUVBUVCUVDYRUUCUHZYTUUEUHZSUVCUVBUVDUVEUVFUV
      CUVDUVESZUVFYTUUBYSOZUHZUVCUVEUVIUVFVHUVDUVEUVHUUEYTUVEUUBYSUUDYRUUCXMUTW
      AWBVFUVGUUPUUJUUBYRRZSZUVIUVCUPUVDUUPUVEYPUUGUUPUUTXPYDUUGUUPYDUUGXPUUPYD
      UUGUUKXPUUPUPZUUNUUHUVLUUJUUSVFVEVQWCWDWEYDUVAUVEUVKXPYDUVEUVAUVKYDUVESZU
      VAUVKUVMUUGUUJUUTUVJYDUUGUUJUPUVEYDUUGUUKUUJUUNUUJUUHWFVEWEYDUUTUUBUUCRZU
      VEUVJYDUUTUVNUUCBRZSZUVNYBUVPAUUBYCXJUUBUHZXTUVNYAUVOUVQXJUUBXSUUCUVQUSXJ
      UUBXRUTZVAUVQXSUUCBUVRVBUKVCUVNUVOWFVEUVEUVJUVNYRUUCUUBUIWGWHWIVRWJWKUUPU
      VKSUVIUVCYRCYQUUBYSWLWMWNWOWPYRYTUUCUUEYQXRWQYQYSWQUUBUUDWQWRWSUVCYRUUCYT
      UUEYQUUBXRUTZUVCYTYQUUDOUUEUVCYQYSUUDUVCYRUUCXMUVSWTWAYQUUBUUDUTXAXBXCXDX
      EXFXDXGXHXI $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( An upper bound for the cardinality of a union.  Theorem 10.47 of
       [TakeutiZaring] p. 98. $)
    unidomg $p |- ( ( A e. C /\ B e. D /\ A. x e. A x ~<_ B ) ->
                   U. A ~<_ ( A X. B ) ) $=
      ( vz vy wcel cv cdom wbr wral cuni cxp wi wceq raleq unieq xpeq1 breq12d
      imbi12d breq2 ralbidv xpeq2 breq2d visset unidom vtocl2g 3impia ) BDHCEHA
      IZCJKZABLZBMZBCNZJKZUJFIZJKZAGIZLZURMZURUPNZJKZOUQABLZUMBUPNZJKZOULUOOGFB
      CDEURBPZUSVCVBVEUQAURBQVFUTUMVAVDJURBRURBUPSTUAUPCPZVCULVEUOVGUQUKABUPCUJ
      JUBUCVGVDUNUMJUPCBUDUEUAAURUPGUFFUFUGUHUI $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.
    uniimadom.1 $e |- A e. _V $.
    uniimadom.2 $e |- B e. _V $.
    $( An upper bound for the cardinality of the union of an image.  Theorem
       10.48 of [TakeutiZaring] p. 99. $)
    uniimadom $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vy wfun cv cfv cdom wbr wral wa cima cuni cxp cvv wcel funimaex adantr
      wi wceq wrex fvelima ex breq1 biimpd reximi r19.36av syl syl6 com23 imp
      r19.21aiv unidomg mp3an2 syl11anc imadomg ax-mp xpdom1 domtr ) DHZAIDJZCK
      LZABMZNZDBOZPZVHCQZKLZVJBCQZKLZVIVLKLVGVHRSZGIZCKLZGVHMZVKVCVNVFDBETUAVGV
      PGVHVCVFVOVHSZVPUBVCVRVFVPVCVRVDVOUCZABUDZVFVPUBZVCVRVTAVOBDUEUFVTVEVPUBZ
      ABUDWAVSWBABVSVEVPVDVOCKUGUHUIVEVPABUJUKULUMUNUOVNCRSVQVKFGVHCRRUPUQURVCV
      MVFVCVHBKLZVMBRSVCWCUBEBRDUSUTVHBCEFVAUKUAVIVJVLVBUR $.
      $( [25-Mar-2006] $)
  $}

  ${
    $d x z A $.  $d x y z B $.  $d y z F $.
    uniimadomf.1 $e |- ( y e. F -> A. x y e. F ) $.
    uniimadomf.2 $e |- A e. _V $.
    uniimadomf.3 $e |- B e. _V $.
    $( An upper bound for the cardinality of the union of an image.  Theorem
       10.48 of [TakeutiZaring] p. 99.  This version of ~ uniimadom uses a
       bound-variable hypothesis in place of a distinct variable condition. $)
    uniimadomf $p |- ( ( Fun F /\ A. x e. A ( F ` x ) ~<_ B ) ->
                 U. ( F " A ) ~<_ ( A X. B ) ) $=
      ( vz wfun cv cfv cdom wbr wral cima cuni cxp uniimadom ax-17 wcel hbfv
      hbbr wceq fveq2 breq1d cbvral sylan2b ) EJIKZELZDMNZICOECPQCDRMNAKZELZDMN
      ZACOICDEGHSUNUKAICUNITABUJDMABUIEFBKZUIUAATUBUOMUAATUODUAATUCULUIUDUMUJDM
      ULUIEUEUFUGUH $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d y z C $.
    iundom.1 $e |- A e. _V $.
    iundom.2 $e |- B e. _V $.
    iundom.3 $e |- C e. _V $.
    $( An upper bound for the cardinality of an indexed union. ` C ` depends on
       ` x ` and should be thought of as ` C ( x ) ` . $)
    iundom $p |- ( A. x e. A C ~<_ B -> U_ x e. A C ~<_ ( A X. B ) ) $=
      ( vy vz cdom wbr wral cv wcel wceq wa copab cfv ciun cxp cvv fvopab2
      mpan2 breq1d ralbiia cima cuni wfun wfn eqid fnopab2 fnfun ax-mp hbopab1
      uniimadomf mpan iuneq2i funiunfvf eqtr3i syl5eqbr sylbir ) DCJKZABLAMZVCB
      NZHMDOPZAHQZRZCJKZABLZABDSZBCTZJKVHVBABVDVGDCJVDDUANVGDOGAHBDUAUBUCZUDUEV
      IVFBUFUGZVKVJJVFUHZVIVMVKJKVFBUIVNAHBDVFGVFUJUKBVFULUMZAIBCVFVEAHIUNZEFUO
      UPABVGSZVJVMABVGDVLUQVNVQVMOVOAIBVFVPURUMUSUTVA $.
      $( [26-Mar-2006] $)
  $}

