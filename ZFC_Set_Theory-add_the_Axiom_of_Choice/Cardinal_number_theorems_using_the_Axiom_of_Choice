$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cardinal number theorems using the Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.
    $( The value of the cardinal number function.  Definition 10.4 of
       [TakeutiZaring] p. 85.  See ~ cardval2 for a simpler version of its
       value. $)
    cardval $p |- ( card ` A ) = |^| { x e. On | x ~~ A } $=
      ( vz vy cvv wcel ccrd cfv cv cen wbr con0 crab cint wceq copab df-card
      fveq1i wrex numth2 intexrab mpbi breq2 rabbidv inteqd fvopabg mpan2
      syl5eq wn c0 fvprc cab wa visset enref brprc mpbiri biantrud abbidv
      df-rab syl6reqr abid2 syl6eq inton eqtr4d pm2.61i ) BEFZBGHZAIZBJKZALMZNZ
      OVGVHBCIVIDIZJKZALMZNZODCPZHZVLBGVQDCAQRVGVLEFZVRVLOVJALSVSABTVJALUAUBDCB
      VPVLEEVMBOZVOVKVTVNVJALVMBVIJUCUDUEUFUGUHVGUIZVHUJVLBGUKWAVLLNUJWAVKLWAVK
      VILFZAULZLWAWCWBVJUMZAULVKWAWBWDAWAVJWBWAVJVIVIJKVIAUNUOVIBJUPUQURUSVJALU
      TVAALVBVCUEVDVCVEVF $.
      $( [21-Oct-2003] $)

    $( Obsolete proof of ~ cardon . $)
    cardonOLD $p |- ( card ` A ) e. On $=
      ( vx ccrd cfv cv cen wbr con0 crab cint cardval wss c0 wne wcel ssrab2
      cvv fvex eqeltrri intex mpbir oninton mp2an eqeltri ) ACDZBEAFGZBHIZJZHBA
      KZUGHLUGMNZUHHOUFBHPUJUHQOUEUHQUIACRSUGTUAUGUBUCUD $.
      $( [22-Oct-2003] $)

    $( Any set is equinumerous to its cardinal number.  Proposition 10.5 of
       [TakeutiZaring] p. 85. $)
    cardid $p |- ( card ` A ) ~~ A $=
      ( vx ccrd cfv cv cen wbr con0 crab wcel cint cardval wss c0 wne ssrab2
      cvv fvex eqeltrri intex mpbir onint mp2an eqeltri breq1 elrab simprbi
      ax-mp ) ACDZBEZAFGZBHIZJZUIAFGZUIULKZULBALZULHMULNOZUOULJUKBHPUQUOQJUIUOQ
      UPACRSULTUAULUBUCUDUMUIHJUNUKUNBUIHUJUIAFUEUFUGUH $.
      $( [22-Oct-2003] $)

    $( A set is a cardinal number iff it equals its own cardinal number.
       Proposition 10.9 of [TakeutiZaring] p. 85. $)
    oncardOLD $p |- ( E. x A = ( card ` x ) <-> A = ( card ` A ) ) $=
      ( vy cv ccrd cfv wceq wex wss cen wbr cardid breq1 mpbiri entr mpan con0
      crab cint cardval wcel wi cardon onintss ax-mp syl5eqss 3syl sseq1
      mpbird eleq1 cardonle syl eqssd 19.23aiv cvv fvex fveq2 eqeq2d cla4egv
      mpcom impbii ) BADZEFZGZAHZBBEFZGZVDVGAVDBVFVDBVFIVCVFIZVDBVBJKZVFVBJKZVH
      VDVIVCVBJKVBLBVCVBJMNVFBJKVIVJBLVFBVBOPVJVCCDZVBJKZCQRSZVFCVBTVFQUAVJVMVF
      IUBBUCVLVJCVFVKVFVBJMUDUEUFUGBVCVFUHUIVDBQUAZVFBIVDVNVCQUAVBUCBVCQUJNBUKU
      LUMUNBUOUAZVGVEVGVOVFUOUABEUPBVFUOUJNVDVGABUOVBBGVCVFBVBBEUQURUSUTVA $.
      $( [11-Aug-2004] $) $( [22-Oct-2003] $)

    $( The cardinal number of a finite set is a finite ordinal.  (Contributed
       by Paul Chapman, 11-Apr-2009.) $)
    ficardomOLD $p |- ( A e. Fin -> ( card ` A ) e. om ) $=
      ( vx cfn wcel ccrd cfv com cv cen wbr wrex isfi biimpi wi wral cardid
      entr mpan a1i rgen rexim ax-mp biimpri 3syl con0 wb cardon onfin sylib )
      ACDZAEFZCDZUKGDZUJABHZIJZBGKZUKUNIJZBGKZULUJUPBALMUOUQNZBGOUPURNUSBGUSUNG
      DUKAIJUOUQAPUKAUNQRSTUOUQBGUAUBULURBUKLUCUDUKUEDULUMUFAUGUKUHUBUI $.
      $( [11-Apr-2009] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( No member of a cardinal number of a set is equinumerous to the set.
       Proposition 10.6(2) of [TakeutiZaring] p. 85. $)
    cardneOLD $p |- ( A e. ( card ` B ) -> -. A ~~ B ) $=
      ( vx con0 wcel ccrd cfv cen wbr wn cardon oneli wss cv crab cint breq1
      onintss cardval sseq1i syl6ibr wb ontri1 mpan sylibd con2d mpcom ) ADEZAB
      FGZEZABHIZJUIABKZLUHUKUJUHUKUIAMZUJJZUHUKCNZBHIZCDOPZAMUMUPUKCAUOABHQRUIU
      QACBSTUAUIDEUHUMUNUBULUIAUCUDUEUFUG $.
      $( [22-Oct-2003] $)
  $}

  $( Two sets are equinumerous iff their cardinal numbers are equal.  This
     important theorem expresses the essential concept behind "cardinality" or
     "size."  This theorem appears as Proposition 10.10 of [TakeutiZaring]
     p. 85, Theorem 7P of [Enderton] p. 197, and Theorem 9 of [Suppes] p. 242
     (among others).  The Axiom of Choice is required for its proof.

     The theory of cardinality can also be developed without AC by introducing
     "card" as a primitive notion and stating this theorem as an axiom, as is
     done with the axiom for cardinal numbers in [Suppes] p. 111.  Finally, if
     we allow the Axiom of Regularity, we can avoid AC by defining the cardinal
     number of a set as the set of all sets equinumerous to it and having least
     possible rank (see ~ karden ). $)
  carden $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) = ( card ` B ) <-> A ~~ B ) ) $=
    ( wcel wa ccrd cfv wceq cen wbr wi cardid ensymg mpi breq2 entr mpan2
    syl6bi syl5com adantr wss mpan wn cardne con2i con0 wb cardon ontri1 mp2an
    sylibr syl syl6 a1i jcad eqss syl6ibr adantl impbid ) ACEZBDEZFAGHZBGHZIZAB
    JKZVAVEVFLVBVAAVCJKZVEVFVAVCAJKZVGAMZVCACNOVEVGAVDJKZVFVCVDAJPVJVDBJKZVFBMZ
    AVDBQRSTUAVBVFVELVAVBVFVCVDUBZVDVCUBZFVEVBVFVMVNVBVFBAJKZVMABDNVOVDAJKZVMVK
    VOVPVLVDBAQUCVPVDVCEZUDZVMVQVPVDAUEUFVCUGEZVDUGEZVMVRUHAUIZBUIZVCVDUJUKULUM
    UNVFVNLVBVFVCBJKZVNVHVFWCVIVCABQUCWCVCVDEZUDZVNWDWCVCBUEUFVTVSVNWEUHWBWAVDV
    CUJUKULUMUOUPVCVDUQURUSUT $.
    $( [22-Oct-2003] $)

  $( Only the empty set has cardinality zero. $)
  cardeq0 $p |- ( A e. B -> ( ( card ` A ) = (/) <-> A = (/) ) ) $=
    ( wcel ccrd cfv c0 wceq cen wbr cvv wb 0ex carden mpan2 card0 eqeq2i en0
    3bitr3g ) ABCZADEZFDEZGZAFHIZTFGAFGSFJCUBUCKLAFBJMNUAFTOPAQR $.
    $( [23-Apr-2004] $)

  ${
    $d x A $.  $d x V $.
    $( A set has cardinality one iff it is a singleton. $)
    card1OLD $p |- ( ( card ` A ) = 1o <-> E. x A = { x } ) $=
      ( ccrd cfv c1o wceq cen wbr cv csn wex cvv wcel c0 wne wn 1n0 df-ne mpbi
      eqeq1 mtbiri fvprc nsyl2 relen brrelexi com 1onn cardnn ax-mp eqeq2i wb
      carden mpan2 syl5bbr pm5.21nii en1 bitri ) BCDZEFZBEGHZBAIJFAKUSBLMZUTUSU
      RNFZVAUSVBENFZENOVCPQENRSURENTUABCUBUCBEGUDUEUSURECDZFZVAUTVDEUREUFMZVDEF
      UGEUHUIUJVAVFVEUTUKUGBELUFULUMUNUOABUPUQ $.
      $( [16-May-2008] $)

    $( A singleton has cardinality one. $)
    cardsnOLD $p |- ( A e. V -> ( card ` { A } ) = 1o ) $=
      ( vx wcel csn cv wceq wex ccrd cfv c1o eqid sneq eqeq2d cla4egv mpi
      card1 sylibr ) ABDZAEZCFZEZGZCHZTIJKGSTTGZUDTLUCUECABUAAGUBTTUAAMNOPCTQR
      $.
      $( [23-Apr-2004] $)
  $}

  ${
    unsnen.1 $e |- A e. _V $.
    unsnen.2 $e |- B e. _V $.
    $( Equinumerosity of a set with a new element added. $)
    unsnen $p |- ( -. B e. A -> ( A u. { B } ) ~~ suc ( card ` A ) ) $=
      ( wcel wn csn cun ccrd cfv csuc cen cin c0 wceq wbr disjsn word cardon
      onordi orddisj ax-mp wa cardid ensymi cvv fvex en2sn mp2an unen mpanl12
      mpan2 sylbir df-suc syl6breqr ) BAEFZABGZHZAIJZUSGZHZUSKLUPAUQMNOZURVALPZ
      ABQVBUSUTMNOZVCUSRVDUSASTUSUAUBAUSLPUQUTLPZVBVDUCVCUSACAUDUEBUFEUSUFEVEDA
      IUGBUSUFUFUHUIAUSUQUTUJUKULUMUSUNUO $.
      $( [7-Nov-2008] $)
  $}

  $( Two sets have the dominance relationship if their cardinalities have the
     subset relationship. $)
  carddomi $p |- ( A e. V -> ( ( card ` A ) C_ ( card ` B ) -> A ~<_ B ) ) $=
    ( wcel ccrd cfv wss cdom wbr wa cen cardid ensymg mpi con0 wi cardon
    ssdomg ax-mp endomtr syl2an domentr sylancl ex ) ACDZAEFZBEFZGZABHIZUEUHJAU
    GHIZUGBKIUIUEAUFKIZUFUGHIZUJUHUEUFAKIUKALUFACMNUFODUHULPAQUFUGORSAUFUGTUABL
    AUGBUBUCUD $.
    $( [22-Oct-2003] $)

  $( Two sets have the dominance relationship iff their cardinalities have the
     subset relationship.  Equation i of [Quine] p. 232. $)
  carddom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) C_ ( card ` B ) <-> A ~<_ B ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr wi carddomi adantr csdm cen wo brdom2 wn
    cardon onelssi syl5 domnsym syl6 con2d con0 wb ontri1 mp2an syl6ibr adantl
    wceq carden eqimss syl6bir jaod syl5bi impbid ) ACEZBDEZFZAGHZBGHZIZABJKZUS
    VDVELUTABCMNVEABOKZABPKZQVAVDABRVAVFVDVGUTVFVDLUSUTVFVCVBEZSZVDUTVHVFUTVHBA
    JKZVFSVHVCVBIUTVJVBVCATZUABADMUBBAUCUDUEVBUFEVCUFEVDVIUGVKBTVBVCUHUIUJUKVAV
    GVBVCULVDABCDUMVBVCUNUOUPUQUR $.
    $( [22-Oct-2003] $)

  $( Two sets have the strict dominance relationship iff their cardinalities
     have the membership relationship.  Corollary 19.7(2) of [Eisenberg]
     p. 310. $)
  cardsdom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) e. ( card ` B ) <-> A ~< B ) ) $=
    ( wcel wa ccrd cfv wss wne cdom wbr cen wn csdm carddom carden necon3abid
    anbi12d con0 wb cardon onelpss mp2an brsdom 3bitr4g ) ACEBDEFZAGHZBGHZIZUHU
    IJZFZABKLZABMLZNZFUHUIEZABOLUGUJUMUKUOABCDPUGUNUHUIABCDQRSUHTEUITEUPULUAAUB
    BUBUHUIUCUDABUEUF $.
    $( [22-Oct-2003] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice. $)
  domtri $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr csdm wn carddom con0 wb cardon ontri1
    mp2an cardsdom ancoms notbid syl5bb bitr3d ) ACEZBDEZFZAGHZBGHZIZABJKBALKZM
    ZABCDNUJUIUHEZMZUGULUHOEUIOEUJUNPAQBQUHUIRSUGUMUKUFUEUMUKPBADCTUAUBUCUD $.
    $( [4-Jan-2004] $)

  $( Trichotomy of equinumerosity and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  Theorem 8 of [Suppes] p. 242. $)
  entric $p |- ( ( A e. V /\ B e. W ) -> ( A ~< B \/ A ~~ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen wo w3o wn cdom domtri biimprd brdom2 syl6ib con1d
    orrd df-3or sylibr ) ACEBDEFZABGHZABIHZJZBAGHZJUCUDUFKUBUEUFUBUFUEUBUFLZABM
    HZUEUBUHUGABCDNOABPQRSUCUDUFTUA $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance and strict dominance. $)
  entri2 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen w3o cdom wo entric brdom2 orbi1i df-3or bitr4i
    sylibr ) ACEBDEFABGHZABIHZBAGHZJZABKHZUALZABCDMUDSTLZUALUBUCUEUAABNOSTUAPQR
    $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance.  This theorem is equivalent to the Axiom of
     Choice.  Part of Proposition 4.42(d) of [Mendelson] p. 275. $)
  entri3 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~<_ A ) ) $=
    ( wcel wa cdom wbr csdm wo entri2 sdomdom orim2i syl ) ACEBDEFABGHZBAIHZJOB
    AGHZJABCDKPQOBALMN $.
    $( [13-Nov-2004] $) $( [4-Jan-2004] $)

  $( If ` A ` is equinumerous to a natural number, then that number is its
     cardinal.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
  cardennnOLD $p |- ( ( A e. V /\ A ~~ B /\ B e. om ) -> ( card ` A ) = B ) $=
    ( wcel cen wbr com w3a ccrd cfv wceq carden exbiri com23 3imp cardnn
    3ad2ant3 eqtrd ) ACDZABEFZBGDZHAIJZBIJZBSTUAUBUCKZSUATUDSUAUDTABCGLMNOUASUC
    BKTBPQR $.
    $( [2-Sep-2009] $)

  $( The cardinality of the successor of an infinite ordinal. $)
  cardsucinfOLD $p |- ( ( A e. On /\ om C_ A )
         -> ( card ` suc A ) = ( card ` A ) ) $=
    ( con0 wcel com wss wa ccrd cfv csuc wceq cen wbr infensuc wb suceloni
    carden mpdan adantr mpbird eqcomd ) ABCZDAEZFZAGHZAIZGHZUCUDUFJZAUEKLZAMUAU
    GUHNZUBUAUEBCUIAOAUEBBPQRST $.
    $( [15-Nov-2008] $)

  $( Strict dominance of a set over a natural number is the same as dominance
     over its successor.  The proof uses AC and Infinity.  It is unclear if a
     proof without using these is possible, unlike the weaker versions
     ~ omsucdomOLD , ~ sucdomiOLD , and ~ fisucdomOLD . $)
  sucdomOLD $p |- ( ( A e. om /\ B e. V ) ->
                   ( A ~< B <-> suc A ~<_ B ) ) $=
    ( com wcel wa cdom wbr csdm wo csuc wb cvv omex entri2 mpan adantl wi
    nnsdom sdomdomtr expdimp sylan2 ancoms peano2b sylbi sdomdom domtr ex 3syl
    adantr jcad pm5.1 syl6 cfn isfinite2 fisucdomOLD syl5 jaod mpd ) ADEZBCEZFZ
    DBGHZBDIHZJZABIHZAKZBGHZLZVAVEUTDMEVAVENDBMCOPQVBVCVIVDVBVCVFVHFVIVBVCVFVHV
    AUTVCVFRZUTVAADIHZVJASVAVKVCVFADBCTUAUBUCUTVCVHRZVAUTVGDIHZVGDGHZVLUTVGDEVM
    AUDVGSUEVGDUFVNVCVHVGDBUGUHUIUJUKVFVHULUMUTVDVIRVAVDBUNEZUTVIBUOUTVOVIABUPU
    HUQUJURUS $.
    $( [13-Sep-2008] $) $( [26-Jul-2004] $)

  ${
    $d x y z w v u t f A $.  $d x y z w v u t f B $.
    unxpdomlemOLD.1 $e |- A e. _V $.
    unxpdomlemOLD.2 $e |- B e. _V $.
    $( Lemma for ~ unxpdomOLD . $)
    unxpdomlemOLD $p |- ( ( 1o ~< A /\ 1o ~< B ) -> ( A u. B ) ~<_ ( A X. B ) )
        $=
      ( vu vw vt vv vx vy vz vf c1o csdm wbr cv wceq wn wrex cun cxp cdom c2o
      csuc com wcel cvv wb 1onn sucdom mp2an df-2o breq1i bitr4i 2dom sylbi wa
      wi cif copab2 crn wss wo elun eqid iftrue ax-mp id 3eqtr4a eqeq1 ifeq2
      ifbieq2d eqeq1d ifbid eqtrd ifeq12 syl11anc rcla42ev syl3an3 3exp com3l
      ad2antlr iffalse sylan9eqr exp4a imp3a adantlr pm2.61d adantl equcomi
      com24 imp syl5eq com23 adantr jaod syl5bi eqcom 2rexbii visset ifex
      elrnoprab syl6ib ssrdv unex ssdomg wfn xpex fnoprab2 fnrndomg mp2 domtr
      mpan2 3syl exp43 r19.23aivv r19.23advv syl2an ) MANOZEPZFPZQZRZFASEASZGPZ
      HPZQZRZHBSGBSZABTZABUAZUBOZMBNOZXSUCAUBOZYDXSMUDZAUBOZYNMUEUFZAUGUFXSYPUH
      UICMAUGUJUKUCYOAUBULUMUNEFACUOUPYMUCBUBOZYIYMYOBUBOZYRYQBUGUFYMYSUHUIDMBU
      GUJUKUCYOBUBULUMUNGHBDUOUPYDYIYLYDYHYLGHBBYCYEBUFZYFBUFZUQZYHYLURUREFAAXT
      AUFZYAAUFZUQZYCUUBYHYLUUEYCUQZUUBYHUQZUQZYJIPZAUFJPZBUFUQKPUUIYAQZUUJYFQZ
      YAUUJUSZUULYFUUIUSZUSZQUQIJKUTZVAZVBZYJUUQUBOZYLUUHLYJUUQUUHLPZYJUFZUUOUU
      TQZJBSIASZUUTUUQUFZUVAUUTAUFZUUTBUFZVCUUHUVCUUTABVDUUHUVEUVCUVFUUGUVEUVCU
      RZUUFUUGUUTYAQZUVGUUAUVHUVGURYTYHUVEUUAUVHUVCUVEUUAUVHUVCUVHUVEUUAUVHYFYF
      QZYAYFUSZUVIYFUUTUSZUSZUUTQZUVCUVHUVJYAUVLUUTUVIUVJYAQYFVEUVIYAYFVFVGUVHU
      VJUVKVFUVHVHVIUVBUVMUVHUUMUULYFUUTUSZUSZUUTQZIJUUTYFABUUIUUTQZUUOUVOUUTUV
      QUUKUVHUUNUVNUUMUUIUUTYAVJUULUUIUUTYFVKVLVMZUULUVOUVLUUTUULUUMUVJQUVNUVKQ
      UVOUVLQUULUUMUVIYAUUJUSUVJUULUULUVIYAUUJUUJYFYFVJZVNUVIUUJYFYAVKVOUULUULU
      VIYFUUTUVSVNUVHUUMUVJUVNUVKVPVQVMVRVSVTWAWBYTYHUVHRZUVGURUUAUVEYTYHUQUVTU
      VCUVEYTYHUVTUVCURUVEYTYHUVTUVCUVEYTYHUVTUQZUVCUWAUVEYTUVHYGYAYEUSZYGYFUUT
      USZUSZUUTQZUVCUVTYHUWDUWCUUTUVHUWBUWCWCYGYFUUTWCWDUVBUWEUVPIJUUTYEABUVRUU
      JYEQZUVOUWDUUTUWFUUMUWBQUVNUWCQUVOUWDQUWFUUMYGYAUUJUSUWBUWFUULYGYAUUJUUJY
      EYFVJZVNYGUUJYEYAVKVOUWFUULYGYFUUTUWGVNUVHUUMUWBUVNUWCVPVQVMVRVSVTWEWFWAW
      GWHWIUUFUVFUVCURZUUGUUFUUTYFQZUWHUUCYCUWIUWHURZUUDUUCYCUWJUUCUVFUWIYCUVCU
      UCUVFUWIYCUVCUUCUVFUWIYCUQZUVCUWKUUCUVFYBUWIYAUUTUSZUWIYFXTUSZUSZUUTQZUVC
      YCUWIUWNUWMUUTYBUWLUWMWCUWIUWMYFUUTUWIYFXTVFLHWJVOWDUVBUWOYBUUMUULYFXTUSZ
      USZUUTQIJXTUUTABUUIXTQZUUOUWQUUTUWRUUKYBUUNUWPUUMUUIXTYAVJUULUUIXTYFVKVLV
      MUUJUUTQZUWQUWNUUTUWSUUMUWLQZUWPUWMQUWQUWNQUWSUUMUWIYAUUJUSUWLUWSUULUWIYA
      UUJUUJUUTYFVJZVNUWIUUJUUTYAVKVOZUWSUULUWIYFXTUXAVNYBUUMUWLUWPUWMVPVQVMVRV
      SVTWEWKWLWGUUDUWIRZUWHURUUCYCUUDUVFUXCUVCUUDUVFUXCUVCUXCUUDUVFYAYAQZUWLUW
      IYFYAUSZUSZUUTQZUVCUXCUXFUWLUUTUXDUXFUWLQYAVEUXDUWLUXEVFVGUWIYAUUTWCWMUVB
      UXGUXDUUMUULYFYAUSZUSZUUTQIJYAUUTABUUKUUOUXIUUTUUKUUKUXDUUNUXHUUMUUIYAYAV
      JUULUUIYAYFVKVLVMUWSUXIUXFUUTUWSUWTUXHUXEQUXIUXFQUXBUWSUULUWIYFYAUXAVNUXD
      UUMUWLUXHUXEVPVQVMVRVSVTWNWBWHWOWPWQUVCUUTUUOQZJBSIASUVDUVBUXJIJABUUOUUTW
      RWSIJKABUUOUUTUUPUUKUUMUUNUULYAUUJFWTJWTXAUULYFUUIHWTIWTXAXAZUUPVEZXBUNXC
      XDYJUGUFUURUUSURABCDXEYJUUQUGXFVGUUSUUQYKUBOZYLYKUGUFUUPYKXGUXMABCDXHIJKA
      BUUOUUPUXKUXLXIYKUGUUPXJXKYJUUQYKXLXMXNXOXPXQWLXR $.
      $( [2-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Cross product dominates union for sets with cardinality greater than 1.
       Proposition 10.36 of [TakeutiZaring] p. 93. $)
    unxpdomOLD $p |- ( ( 1o ~< A /\ 1o ~< B ) -> ( A u. B ) ~<_ ( A X. B ) ) $=
      ( vx vy cvv wcel wa c1o csdm wbr cun cxp cdom sdomex simprd anim12i cv
      wi wceq breq2 anbi1d uneq1 xpeq1 breq12d imbi12d anbi2d uneq2 xpeq2
      visset unxpdomlemOLD vtocl2g mpcom ) AEFZBEFZGHAIJZHBIJZGZABKZABLZMJZUOUM
      UPUNUOHEFZUMHANOUPVAUNHBNOPHCQZIJZHDQZIJZGZVBVDKZVBVDLZMJZRUOVEGZAVDKZAVD
      LZMJZRUQUTRCDABEEVBASZVFVJVIVMVNVCUOVEVBAHITUAVNVGVKVHVLMVBAVDUBVBAVDUCUD
      UEVDBSZVJUQVMUTVOVEUPUOVDBHITUFVOVKURVLUSMVDBAUGVDBAUHUDUEVBVDCUIDUIUJUKU
      L $.
      $( [15-Sep-2004] $)
  $}

  $( Strict dominance implies ordinal membership. $)
  sdomelOLD $p |- ( ( A e. On /\ B e. On ) -> ( A ~< B -> A e. B ) ) $=
    ( con0 wcel csdm wbr wi wa wss cdom wn ssdomg adantr ontri1 domtri 3imtr3d
    con4d ancoms ) BCDZACDZABEFZABDZGSTHZUBUAUCBAIZBAJFZUBKUAKSUDUEGTBACLMBANBA
    CCOPQR $.
    $( [21-Nov-2004] $) $( [4-Nov-2003] $)

  $( A set strictly dominates iff its cardinal strictly dominates. $)
  sdomsdomcard $p |- ( A ~< B <-> A ~< ( card ` B ) ) $=
    ( csdm wbr cvv wcel ccrd cfv sdomex simprd wn c0 sdom0 fvprc breq2d mtbiri
    con4i cen cardid ensymg mpi wa wi fvex sdomentr ax-mp ex syl5com mpan2i
    impbid pm5.21nii ) ABCDZBEFZABGHZCDZULAEFUMABIJUMUOUMKZUOALCDAMUPUNLACBGNOP
    QUMULUOUMBUNRDZULUOUMUNBRDZUQBSZUNBETUAULUQUOUNEFULUQUBUOUCBGUDABUNEUEUFUGU
    HUMUOURULUSAUNBEUEUIUJUK $.
    $( [30-Oct-2003] $)

  $( The cardinality function is idempotent.  Proposition 10.11 of
     [TakeutiZaring] p. 85. $)
  cardidmOLD $p |- ( card ` ( card ` A ) ) = ( card ` A ) $=
    ( cvv wcel ccrd cfv wceq cen wbr cardid wb fvex carden mpan mpbiri wn c0
    card0 fvprc fveq2d 3eqtr4a pm2.61i ) ABCZADEZDEZUCFZUBUEUCAGHZAIUCBCUBUEUFJ
    ADKUCABBLMNUBOZPDEPUDUCQUGUCPDADRZSUHTUA $.
    $( [30-Oct-2003] $)

  $( Cantor's theorem in terms of cardinals.  This theorem tells us that no
     matter how largei a cardinal number is, there is a still larger cardinal
     number.  Theorem 18.12 of [Monk1] p. 133. $)
  canth3 $p |- ( A e. V -> ( card ` A ) e. ( card ` ~P A ) ) $=
    ( wcel ccrd cfv cpw csdm wbr canth2g cvv wb pwexg cardsdom mpdan mpbird )
    ABCZADEAFZDECZAQGHZABIPQJCRSKABLAQBJMNO $.
    $( [11-Jul-2004] $) $( [5-Nov-2003] $)

  ${
    $d x A $.
    $( An infinite cardinal is a limit ordinal.  Equivalent to Exercise 4 of
       [TakeutiZaring] p. 91. $)
    cardlimOLD $p |- ( om C_ ( card ` A ) <-> Lim ( card ` A ) ) $=
      ( vx com ccrd cfv wss wlim cv csuc wceq con0 wrex wn wcel wa cen wbr wi
      sseq2 biimpd wb limom limsssuc ax-mp infensuc ex syl5bir sylan9r breq2
      adantl sylibrd com3r imp visset sucid eleq2 mpbiri cardidm syl6eleqr
      cardne syl a1i pm2.65d nrexdv c0 wo peano1 ssel mpi n0i w3o word cardon
      onordi ordzsl mpbi 3orass ori 3syl ord mpd limomss impbii ) CADEZFZWDGZWE
      WDBHZIZJZBKLZMWFWEWIBKWEWGKNZOZWIWGWDPQZWEWKWIWMRWKWIWEWMWKWIWEWMRWKWIOWE
      WGWHPQZWMWIWECWHFZWKWNWIWEWOWDWHCSTWOCWGFZWKWNCGWPWOUAUBCWGUCUDWKWPWNWGUE
      UFUGUHWIWMWNUAWKWDWHWGPUIUJUKUFULUMWIWMMZRWLWIWGWDDEZNWQWIWGWDWRWIWGWDNWG
      WHNWGBUNUOWDWHWGUPUQAURUSWGWDUTVAVBVCVDWEWJWFWEVEWDNZWDVEJZMWJWFVFZWEVECN
      WSVGCWDVEVHVIWDVEVJWTXAWTWJWFVKZWTXAVFWDVLXBWDAVMVNBWDVOVPWTWJWFVQVPVRVSV
      TWAWDWBWC $.
      $( [3-Jun-2004] $) $( [30-Oct-2003] $)
  $}

  $( A cardinal strictly dominates its members.  Equivalent to Proposition
     10.37 of [TakeutiZaring] p. 93 (use ~ cardsdom to obtain the exact
     proposition from this one). $)
  cardsdomel $p |- ( A e. On -> ( A ~< B <-> A e. ( card ` B ) ) ) $=
    ( csdm wbr ccrd cfv con0 wcel sdomsdomcard wss cdom wn ssdom2g wb cardon
    ontri1 mpan domtri 3imtr3d con4d cen onelssi wi ax-mp syl cardidm eleq2i
    cardne sylbir brsdom sylanbrc impbid1 syl5bb ) ABCDABEFZCDZAGHZAUNHZABIUPUO
    UQUPUQUOUPUNAJZUNAKDZUQLZUOLZUNAGMUNGHZUPURUTNBOZUNAPQVBUPUSVANVCUNAGGRQSTU
    QAUNKDZAUNUADLZUOUQAUNJZVDUNAVCUBVBVFVDUCVCAUNGMUDUEUQAUNEFZHVEVGUNABUFUGAU
    NUHUIAUNUJUKULUM $.
    $( [2-Nov-2003] $)

  ${
    $d x A $.
    $( Two ways to express the property of being a cardinal number. $)
    iscardOLD $p |- ( ( card ` A ) = A <-> ( A e. On /\ A. x e. A x ~< A ) ) $=
      ( ccrd cfv wceq con0 wcel wa cv csdm wbr wral cardon eleq1 mpbii
      pm4.71ri wss wb cardonle eqss baibr syl onelon cardsdomel ralbidva dfss3
      syl6rbbr bitr3d pm5.32i bitri ) BCDZBEZBFGZULHUMAIZBJKZABLZHULUMULUKFGUMB
      MUKBFNOPUMULUPUMBUKQZULUPUMUKBQZUQULRBSULURUQUKBTUAUBUMUPUNUKGZABLUQUMUOU
      SABUMUNBGHUNFGUOUSRBUNUCUNBUDUBUEABUKUFUGUHUIUJ $.
      $( [2-Nov-2003] $)
  $}

  ${
    $d x y A $.
    $( Two ways to express the property of being a cardinal number.  Definition
       8 of [Suppes] p. 225. $)
    iscard2OLD $p |- ( ( card ` A ) = A <->
                  ( A e. On /\ A. x e. On ( A ~~ x -> A C_ x ) ) ) $=
      ( vy ccrd cfv wceq con0 wcel wa cv cen wbr wss wi wral cardon eleq1
      mpbii pm4.71ri cardonle biantrurd eqss syl6rbbr crab cint cardval sseq2i
      ssint breq1 elrab ensymg visset ensym impbid1 anbi2d syl5bb imbi1d
      impexp syl6bb ralbidv2 bitrd pm5.32i bitri ) BDEZBFZBGHZVEIVFBAJZKLZBVGMZ
      NZAGOZIVEVFVEVDGHVFBPVDBGQRSVFVEVKVFVEBVDMZVKVFVLVDBMZVLIVEVFVMVLBTUAVDBU
      BUCVLBCJZBKLZCGUDZUEZMZVFVKVDVQBCBUFUGVRVIAVPOVFVKABVPUHVFVIVJAVPGVFVGVPH
      ZVINVGGHZVHIZVINVTVJNVFVSWAVIVSVTVGBKLZIVFWAVOWBCVGGVNVGBKUIUJVFWBVHVTVFW
      BVHVGBGUKBVGAULUMUNUOUPUQVTVHVIURUSUTUPUPVAVBVC $.
      $( [27-Nov-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x A $.
    $( An alternate version of the value of the cardinal number of a set.
       Compare ~ cardval .  This theorem could be used to give us a simpler
       definition of ` card ` in place of ~ df-card .  It apparently does not
       occur in the literature. $)
    cardval2 $p |- ( card ` A ) = { x e. On | x ~< A } $=
      ( cv csdm wbr con0 crab wcel wa cab ccrd cfv df-rab cardon oneli
      pm4.71ri cardsdomel pm5.32i bitr4i abbii abid2 3eqtr2ri ) ACZBDEZAFGUCFHZ
      UDIZAJUCBKLZHZAJUGUDAFMUHUFAUHUEUHIUFUHUEUGUCBNOPUEUDUHUCBQRSTAUGUAUB $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The collection of ordinal numbers dominated by a set is an ordinal
       number.  (In general, not all collections of ordinal numbers are
       ordinal.)  Theorem 56 of [Suppes] p. 227.  This theorem can be proved
       (with a longer proof) without the Axiom of Choice; see ~ hartogs . $)
    ondomon $p |- ( A e. V -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz wcel cv cdom wbr con0 crab word wtr wss wa wi wal onelon onelss
      imp cvv visset ssdomg ax-mp syl jca domtr anim2i anassrs sylan exp31
      com12 imp3a breq1 elrab 3imtr4g gen2 dftr2 mpbir ssrab2 ordon trssord
      mp3an cpw csdm wb wral elisset canth2g domsdomtr sylan2 expcom a1d
      r19.21aiv ss2rab sylibr ccrd cfv cardval2 fvex eqeltrri ssex elong 3syl
      mpbiri ) BCFZAGZBHIZAJKZJFZWILZWIMZWIJNJLWKWLDGZEGZFZWNWIFZOWMWIFZPZEQDQW
      RDEWOWPWQWOWNJFZWNBHIZOWMJFZWMBHIZOZWPWQWOWSWTXCWSWOWTXCPWSWOWTXCWSWOOZXA
      WMWNHIZOWTXCXDXAXEWNWMRXDWMWNNZXEWSWOXFWNWMSTWMUAFXFXEPDUBWMWNUAUCUDUEUFX
      AXEWTXCXEWTOXBXAWMWNBUGUHUIUJUKULUMWHWTAWNJWGWNBHUNUOWHXBAWMJWGWMBHUNUOUP
      TUQDEWIURUSWHAJUTVAWIJVBVCWFWIWGBVDZVEIZAJKZNZWIUAFWJWKVFWFWHXHPZAJVGZXJW
      FBUAFZXLBCVHXMXKAJXMXKWGJFWHXMXHXMWHBXGVEIXHBUAVIWGBXGVJVKVLVMVNUEWHXHAJV
      OVPWIXIXGVQVRXIUAAXGVSXGVQVTWAWBWIUAWCWDWE $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The class of ordinal numbers dominated by a set is a cardinal number.
       Theorem 59 of [Suppes] p. 228. $)
    ondomcardOLD $p |- ( A e. V ->
                 ( card ` { x e. On | x ~<_ A } ) = { x e. On | x ~<_ A } ) $=
      ( vy wcel cvv cv cdom wbr con0 crab ccrd cfv wceq elisset cen wss wi
      wral ondomon wa wn visset ensym csdm breq1 elrab simprbi word eloni
      ordirr syl hbrab1 ax-17 hbbr elrabf biimpri mtand wb domtri con2bid
      mpancom mpbird domsdomtr syl2an sdomnen expcom con2d syl5 adantr ontri1
      sylan sylibrd r19.21aiva iscard2 sylanbrc ) BCEBFEZAGZBHIZAJKZLMVTNZBCOVQ
      VTJEZVTDGZPIZVTWCQZRZDJSWAABFTZVQWFDJVQWCJEZUAWDWCVTEZUBZWEVQWDWJRWHWDWCV
      TPIZVQWJVTWCDUCUDVQWIWKWIVQWKUBZWIVQUAWCVTUEIZWLWIWCBHIZBVTUEIZWMVQWIWHWN
      VSWNAWCJVRWCBHUFUGUHVQWOVTBHIZUBZVQWBWQWGWBWPVTVTEZWBVTUIWRUBVTUJVTUKULWR
      WBWPUAVSWPADVTJVSADJUMZWHAUNADVTBHWSWCHEAUNWCBEAUNUOVRVTBHUFUPUQURULWBVQW
      OWQUSWGWBVQUAWPWOVTBJFUTVAVBVCWCBVTVDVEWCVTVFULVGVHVIVJVQWBWHWEWJUSWGVTWC
      VKVLVMVNDVTVOVPUL $.
      $( [2-Dec-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The union of a set of cardinals is a cardinal.  Theorem 18.14 of [Monk1]
       p. 133. $)
    carduniOLD $p |- ( A e. B ->
                  ( A. x e. A ( card ` x ) = x -> ( card ` U. A ) = U. A ) ) $=
      ( vy wcel cv ccrd cfv wceq wral cuni wa wss wn con0 word wb fveq2 id
      eqeq12d rcla4v cardon eleq1 mpbii syl6com ssrdv ssonuni syl5 imp eloni
      onordi ordtri4 mpan 3syl cardonle syl elirr wi wex eluni cdom wbr
      elssuni cvv visset ssdomg ax-mp uniexg carddom bicomd sseq1 sylan9bb
      syl5ib ssel syl6 ex com13 syld com4r 19.23aiv sylbi mtoi mpbir2and ) BCEZ
      AFZGHZWEIZABJZBKZGHZWIIZWDWHLZWKWJWIMZWJWIEZNZWLWIOEZWIPZWKWMWOLQZWDWHWPW
      HBOMWDWPWHDBODFZBEZWHWSGHZWSIZWSOEZWGXBAWSBWEWSIZWFXAWEWSWEWSGRXDSTUAZXBX
      AOEXCWSUBXAWSOUCUDUEUFBCUGUHUIZWIUJWJPWQWRWJWIUBUKWJWIULUMUNWLWPWMXFWIUOU
      PWLWNWJWJEZWJUQWDWHWNXGURWNWHWDXGWNWJWSEZWTLZDUSWHWDXGURURZDWJBUTXIXJDXHW
      TXJWTWHWDXHXGWTWHXBWDXHXGURZURXEWDXBWTXKWDXBWTXKURWDXBLZWTWSWJMZXKWTWSWIV
      AVBZXLXMWTWSWIMZXNWSBVCWSVDEZXOXNURDVEZWSWIVDVFVGUPWDXNXAWJMZXBXMWDWIVDEZ
      XNXRQBCVHXSXRXNXPXSXRXNQXQWSWIVDVDVIUMVJUPXAWSWJVKVLVMWSWJWJVNVOVPVQVRVSU
      IVTWAVQUIWBWCVP $.
      $( [1-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y V $.
    $( The smallest ordinal that strictly dominates a set is a cardinal. $)
    cardmin $p |- ( A e. V -> ( card ` |^| { x e. On | A ~< x } ) =
                      |^| { x e. On | A ~< x } ) $=
      ( vy wcel cv csdm wbr con0 crab cint wral ccrd cfv wceq wrex numthcor
      onintrab2 sylib cdom wa wn wi onelon ex syl breq2 elrab wss ssrab2
      onnmin mpan sylbir con2d syli cvv wb visset domtri sylibrd ax-17 hbrab1
      hbint hbbr onminsb jctird domsdomtr syl6 r19.21aiv iscard sylanbrc ) BCEZ
      BAFZGHZAIJZKZIEZDFZVPGHZDVPLVPMNVPOVLVNAIPZVQABCQZVNARSZVLVSDVPVLVRVPEZVR
      BTHZBVPGHZUAVSVLWCWDWEVLWCBVRGHZUBZWDWCVLVRIEZWGVLVQWCWHUCWBVQWCWHVPVRUDU
      EUFWHWFWCWHWFWCUBZWHWFUAVRVOEZWIVNWFAVRIVMVRBGUGUHVOIUIWJWIVNAIUJVOVRUKUL
      UMUEUNUOVRUPEVLWDWGUQDURVRBUPCUSULUTVLVTWEWAVNWEAADBVPGVRBEAVAVRGEAVAADVO
      VNADIVBVCVDVMVPBGUGVEUFVFVRBVPVGVHVIDVPVJVK $.
      $( [28-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( The class of all cardinal numbers is not a set (i.e. is a proper
       class).  Theorem 19.8 of [Eisenberg] p. 310. $)
    cardprcOLD $p |- -. { x | ( card ` x ) = x } e. _V $=
      ( vy cv ccrd cfv wceq cab cvv wcel cuni cpw canth3 wss wn cardidm cdom
      wbr fvex wb ax-17 hbab1 hbuni hbpw hbfv hbeq fveq2 id eqeq12d elabgf
      ax-mp mpbir elssuni ssdomg mp2 carddom mpan mpbiri syl5eqssr con0 cardon
      ontri1 mp2an sylib pm2.65i uniexg mto ) ACZDEZVGFZAGZHIVJJZHIZVLVKDEZVKKZ
      DEZIZVKHLVLVOVMMZVPNZVLVOVODEZVMVNOZVLVSVMMZVOVKPQZVOHIZVOVKMZWBVNDRZVOVJ
      IZWDWFVSVOFZVTWCWFWGSWEVIWGABVOHABVNDBCDIATZABVKABVJVIABUAUBUCUDZABBVSVOA
      BVODWHWIUDWIUEVGVOFZVHVSVGVOVGVODUFWJUGUHUIUJUKVOVJULUJVOVKHUMUNWCVLWAWBS
      WEVOVKHHUOUPUQURVOUSIVMUSIVQVRSVNUTVKUTVOVMVAVBVCVDVJHVEVF $.
      $( [31-Oct-2004] $) $( [28-Oct-2003] $)
  $}

  ${
    $d A x $.  $d V x $.
    $( A set is finite iff its cardinal is a natural number.  (Contributed by
       Jeff Madsen 2-Sep-2009.) $)
    ficard $p |- ( A e. V -> ( A e. Fin <-> ( card ` A ) e. om ) ) $=
      ( vx wcel cfn ccrd cfv com cv cen wbr wrex isfi wa wceq carden wi cardnn
      eqtr expcom syl eleq1a syld adantl sylbird r19.23adva syl5bi eqcomd
      mpbid ex ancld breq2 rcla4ev sylibr syl6 impbid ) ABDZAEDZAFGZHDZURACIZJK
      ZCHLZUQUTCAMZUQVBUTCHUQVAHDZNVBUSVAFGZOZUTAVABHPVEVGUTQUQVEVGUSVAOZUTVEVF
      VAOZVGVHQVARVGVIVHUSVFVASTUAVAHUSUBUCUDUEUFUGUQUTUTAUSJKZNZURUQUTVJUQUTVJ
      UQUTNUSUSFGZOZVJUTVMUQUTVLUSUSRUHUDAUSBHPUIUJUKVKVCURVBVJCUSHVAUSAJULUMVD
      UNUOUP $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y z w A $.
    $( Value of the aleph function at a successor ordinal.  Definition 12(ii)
       of [Suppes] p. 91. $)
    alephsucOLD $p |- ( A e. On -> ( aleph ` suc A ) =
                    |^| { x e. On | ( aleph ` A ) ~< x } ) $=
      ( vy vz vw con0 wcel cale cfv cv csdm wbr crab cint cvv csuc wceq wrex
      fvex numthcor ax-mp intexrab mpbi com ax-17 df-aleph breq1 rabbidv
      inteqd rdgsucopab mpan2 ) BFGBHIZAJZKLZAFMZNZOGZBPHIUPQUNAFRZUQULOGURBHSA
      ULOTUAUNAFUBUCCDEUDBCJZUMKLZAFMZNUPOHEJZUDGCUEVBBGCUEVBUPGCUECDAUFUSULQZV
      AUOVCUTUNAFUSULUMKUGUHUIUJUK $.
      $( [22-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Every aleph is a cardinal number.  Theorem 65 of [Suppes] p. 229. $)
    alephcardOLD $p |- ( card ` ( aleph ` A ) ) = ( aleph ` A ) $=
      ( vx vy con0 wcel cale cfv ccrd wceq cv c0 csuc fveq2 fveq2d eqeq12d com
      cardom aleph0 fveq2i 3eqtr4i csdm wbr crab cint cvv fvex cardmin ax-mp
      alephsuc 3eqtr4a a1d wlim wral wa ciun wi visset cardiun adantl alephlim
      mpan adantr 3eqtr4d ex tfinds wn card0 cdm wfn alephfnon fndm eleq2i
      notbii ndmfv sylbir pm2.61i ) ADEZAFGZHGZVRIZBJZFGZHGZWBIZKFGZHGZWEICJZFG
      ZHGZWHIZWGLZFGZHGZWLIZVTBCAWAKIZWCWFWBWEWOWBWEHWAKFMZNWPOWAWGIZWCWIWBWHWQ
      WBWHHWAWGFMZNWROWAWKIZWCWMWBWLWSWBWLHWAWKFMZNWTOWAAIZWCVSWBVRXAWBVRHWAAFM
      ZNXBOPHGPWFWEQWEPHRSRTWGDEZWNWJXCWHWAUAUBBDUCUDZHGZXDWMWLWHUEEXEXDIWGFUFB
      WHUEUGUHXCWLXDHBWGUIZNXFUJUKWAULZWJCWAUMZWDXGXHUNZCWAWHUOZHGZXJWCWBXHXKXJ
      IZXGWAUEEZXHXLUPBUQZCWAWHUEURUHUSXIWBXJHXGWBXJIZXHXMXGXOXNCWAUEUTVAVBZNXP
      VCVDVEVQVFZKHGKVSVRVGXQVRKHXQAFVHZEZVFVRKIXSVQXRDAFDVIXRDIVJDFVKUHVLVMAFV
      NVOZNXTUJVP $.
      $( [25-Oct-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( No cardinal can be sandwiched between an aleph and its successor aleph.
       Theorem 67 of [Suppes] p. 229. $)
    alephnbtwnOLD $p |- ( ( card ` B ) = B ->
                   -. ( ( aleph ` A ) e. B /\ B e. ( aleph ` suc A ) ) ) $=
      ( vx con0 wcel ccrd cfv wceq cale csuc wa wn wi csdm wbr wb alephon
      cardsdomel ax-mp eleq2 syl5bb adantl cardon eleq1 mpbii cv crab cint
      alephsuc eleq2d biimpd breq2 onnminsb sylan9 con2d sylan2 sylbird imnan
      sylib ex c0 n0i cdm wfn alephfnon fndm eleq2i notbii ndmfv sylbir nsyl2
      sucelon sylibr con3i a1d pm2.61i ) ADEZBFGZBHZAIGZBEZBAJZIGZEZKZLZMVQVSWF
      VQVSKZWAWDLZMWFWGWAVTBNOZWHVSWIWAPVQWIVTVREZVSWAVTDEWIWJPAQVTBRSVRBVTTUAU
      BVSVQBDEZWIWHMVSVRDEWKBUCVRBDUDUEVQWKKWDWIVQWDBVTCUFZNOZCDUGUHZEZWKWILVQW
      DWOVQWCWNBCAUIUJUKWMWICBWLBVTNULUMUNUOUPUQWAWDURUSUTVQLWFVSWEVQWDVQWAWDWB
      DEZVQWDWCVAHZWPWCBVBWPLWBIVCZEZLWQWSWPWRDWBIDVDWRDHVEDIVFSVGVHWBIVIVJVKAV
      LVMUBVNVOVP $.
      $( [10-Nov-2003] $)
  $}

  $( No set has equinumerosity between an aleph and its successor aleph. $)
  alephnbtwn2OLD $p |- -. ( ( aleph ` A ) ~< B /\ B ~< ( aleph ` suc A ) ) $=
    ( cvv wcel cale cfv csdm wbr csuc wa wn ccrd wceq cardidm alephnbtwn ax-mp
    wb con0 alephon cardsdomel a1i alephcard eleq2i cardsdom mpan2 syl5rbbr
    anbi12d mtbiri relsdom brrelexi adantl con3i pm2.61i ) BCDZAEFZBGHZBAIZEFZG
    HZJZKUNUTUOBLFZDZVAURDZJZVALFVAMVDKBNAVAOPUNUPVBUSVCUPVBQZUNUORDVEASUOBTPUA
    VCVAURLFZDZUNUSVFURVAUQUBUCUNURRDVGUSQUQSBURCRUDUEUFUGUHUTUNUSUNUPBURGUIUJU
    KULUM $.
    $( [3-Nov-2003] $)

  $( The power set of an aleph dominates the successor aleph.  (The Generalized
     Continuum Hypothesis says they are equinumerous.) $)
  alephsucpw $p |- ( aleph ` suc A ) ~<_ ~P ( aleph ` A ) $=
    ( csuc cale cfv cpw cdom wbr csdm wn fvex canth2 wi wa alephnbtwn2 imnan
    mpbir ax-mp cvv wcel wb pwex domtri mp2an ) ABZCDZACDZEZFGZUGUEHGZIZUFUGHGZ
    UJUFACJZKUKUJLUKUIMIAUGNUKUIOPQUERSUGRSUHUJTUDCJUFULUAUEUGRRUBUCP $.
    $( [27-Aug-2005] $)

  $( The set exponentiation of 2 to the aleph-zero has cardinality of at least
     aleph-one.  (If we were to assume the Continuum Hypothesis, their
     cardinalities would be the same.) $)
  aleph1 $p |- ( aleph ` 1o ) ~<_ ( 2o ^m ( aleph ` (/) ) ) $=
    ( c1o cale cfv c0 csuc c2o cmap co cdom df-1o fveq2i cpw wbr alephsucpw
    cvv wcel cen wb oprex fvex pw2en domen2 mp2an mpbi eqbrtri ) ABCDEZBCZFDBCZ
    GHZIAUFBJKUGUHLZIMZUGUIIMZDNUIOPUJUIQMUKULRFUHGSUHDBTUAUJUIUGOUBUCUDUE $.
    $( [7-Jul-2004] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( Lemma for ~ alephordi . $)
    alephordilem1OLD $p |- ( A e. On -> ( aleph ` A ) ~< ( aleph ` suc A ) ) $=
      ( vx vy con0 wcel cale cfv cv csdm wbr crab cint csuc wrex alephon
      numthcor ax-mp ax-17 hbrab1 hbint hbbr breq2 onminsb alephsuc syl5breqr
      ) ADEAFGZUFBHZIJZBDKZLZAMFGIUHBDNZUFUJIJZUFDEUKAOBUFDPQUHULBBCUFUJICHZUFE
      BRUMIEBRBCUIUHBCDSTUAUGUJUFIUBUCQBAUDUE $.
      $( [26-Oct-2003] $)

    $( Lemma for ~ alephordi . $)
    alephordilem2OLD $p |- ( ( B e. _V /\ Lim B ) ->
                       ( A e. B -> ( aleph ` A ) ~<_ ( aleph ` B ) ) ) $=
      ( vx cvv wcel wlim wa cale cfv wss cdom wbr cv ciun fveq2 ssiun2s
      alephlim sseq2d syl5ibr con0 wi alephon ssdomg ax-mp syl6 ) BDEBFGZABEZAH
      IZBHIZJZUHUIKLZUGUJUFUHCBCMZHIZNZJCBUMAUHULAHOPUFUIUNUHCBDQRSUHTEUJUKUAAU
      BUHUITUCUDUE $.
      $( [26-Oct-2003] $)

    $( Strict ordering property of the aleph function. $)
    alephordiOLD $p |- ( B e. On ->
                    ( A e. B -> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
      ( vx vy cv wcel cale cfv csdm wbr wi c0 csuc wceq eleq2 fveq2 breq2d
      imbi12d noel pm2.21i con0 wo visset elsuc2 alephordilem1OLD sdomtr
      sylan2 expcom imim2d com23 breq1d syl5ibr a1d com3r jaod syl5bi wlim
      wral cdom cen wn wa wss ciun ssiun2s cvv alephlim mpan sseq2d alephon
      ssdomg ax-mp syl6 limsuc alephordilem2OLD sylbid imp domnsym syl limelon
      onelon sylan fvex ensym ensdomtr ex syl5 syl5com mtod jcad brsdom
      syl6ibr tfinds ) ACEZFZAGHZWNGHZIJZKZALFZWPLGHZIJZKADEZFZWPXCGHZIJZKZAXCM
      ZFZWPXHGHZIJZKABFZWPBGHZIJZKCDBWNLNZWOWTWRXBWNLAOXOWQXAWPIWNLGPQRWNXCNZWO
      XDWRXFWNXCAOXPWQXEWPIWNXCGPQRWNXHNZWOXIWRXKWNXHAOXQWQXJWPIWNXHGPQRWNBNZWO
      XLWRXNWNBAOXRWQXMWPIWNBGPQRWTXBASTXCUAFZXIXGXKXIXDAXCNZUBXSXGXKKZXCADUCUD
      XSXDYAXTXSXGXDXKXSXFXKXDXFXSXKXSXFXEXJIJZXKXCUEZWPXEXJUFUGUHUIUJXTXGXSXKX
      TXSXKKXGXSXKXTYBYCXTWPXEXJIAXCGPUKULUMUNUOUPUJWNUQZWSXGDWNURYDWOWPWQUSJZW
      PWQUTJZVAZVBWRYDWOYEYGYDWOWPWQVCZYEWOYHYDWPDWNXEVDZVCDWNXEAWPXCAGPVEYDWQY
      IWPWNVFFZYDWQYINCUCZDWNVFVGVHVIULWPUAFYHYEKAVJWPWQUAVKVLVMYDWOYGYDWOVBZYF
      WQAMZGHZIJZYLYNWQUSJZYOVAYDWOYPYDWOYMWNFZYPWNAVNYJYDYQYPKYKYMWNVOVHVPVQYN
      WQVRVSYLAUAFZYFYOYDWNUAFZWOYRYJYDYSYKWNVFVTVHWNAWAWBYRWPYNIJZYFYOAUEYFWQW
      PUTJZYTYOKWPWQWNGWCWDUUAYTYOWQWPYNWEWFVSWGWHWIWFWJWPWQWKWLUMWM $.
      $( [26-Oct-2003] $)
  $}

  $( Ordering property of the aleph function. $)
  alephordOLD $p |- ( ( A e. On /\ B e. On ) ->
                 ( A e. B <-> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr wi alephordi adantl cdom cen wn brsdom
    wss wne wb alephon domtri mp2an con3d syl5bi adantr ontri1 sylibrd wceq
    fveq2 eqeng ax-mp syl necon3bi a1i anim12d onelpss impbid ) ACDZBCDZEZABDZA
    FGZBFGZHIZUSVAVDJURABKLVDVBVCMIZVBVCNIZOZEZUTVAVBVCPUTVHABQZABRZEVAUTVEVIVG
    VJUTVEBADZOZVIURVEVLJUSVEVCVBHIZOZURVLVBCDZVCCDVEVNSATZBTVBVCCCUAUBURVKVMBA
    KUCUDUEABUFUGVGVJJUTVFABABUHVBVCUHZVFABFUIVOVQVFJVPVBVCCUJUKULUMUNUOABUPUGU
    DUQ $.
    $( [26-Oct-2003] $)

  $( Ordering property of the aleph function.  Theorem 8A(a) of [Enderton]
     p. 213 and its converse. $)
  alephord2OLD $p |- ( ( A e. On /\ B e. On ) ->
                  ( A e. B <-> ( aleph ` A ) e. ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr alephord ccrd wb alephon cardsdomel ax-mp
    alephcard eleq2i bitri syl6bb ) ACDBCDEABDAFGZBFGZHIZTUADZABJUBTUAKGZDZUCTC
    DUBUELAMTUANOUDUATBPQRS $.
    $( [3-Nov-2003] $)

  $( A set dominated by an aleph is strictly dominated by its successor aleph
     and vice-versa. $)
  alephsucdomOLD $p |- ( B e. On ->
                    ( A ~<_ ( aleph ` B ) <-> A ~< ( aleph ` suc B ) ) ) $=
    ( cvv wcel con0 cale cfv cdom wbr csuc csdm wb wi wa alephordilem1OLD
    domsdomtr ex syl5com adantl wn alephnbtwn2 imnan mpbir con2i fvex domtri
    mpan2 syl5ibr adantr impbid reldom brrelexi relsdom pm5.21ni a1d pm2.61i )
    ACDZBEDZABFGZHIZABJFGZKIZLZMUQURVCUQURNUTVBURUTVBMUQURUSVAKIZUTVBBOUTVDVBAU
    SVAPQRSUQVBUTMURVBUTUQUSAKIZTZVEVBVEVBTMVEVBNTBAUAVEVBUBUCUDUQUSCDUTVFLBFUE
    AUSCCUFUGUHUIUJQUQTVCURUTUQVBAUSHUKULAVAKUMULUNUOUP $.
    $( [3-Nov-2003] $)

  ${
    $d x A $.
    $( An alternate representation of a successor aleph.  Using this theorem we
       could define the aleph function with ` { z e. On | z ~<_ x } ` in place
       of ` |^| { z e. On | x ~< z } ` in ~ df-aleph . $)
    alephsuc2OLD $p |- ( A e. On ->
                ( aleph ` suc A ) = { x e. On | x ~<_ ( aleph ` A ) } ) $=
      ( con0 wcel cv cale cfv cdom wbr crab csuc csdm alephsucdom rabbidv ccrd
      alephcard cardval2 eqtr3i syl6reqr ) BCDZAEZBFGHIZACJUABKZFGZLIZACJZUDTUB
      UEACUABMNUDOGUDUFUCPAUDQRS $.
      $( [3-Nov-2003] $)
  $}

  ${
    $d x y A $.
    $( Given any transfinite cardinal number ` A ` , there is exactly one aleph
       that is equal to it.  Here we compute that aleph _explicitly_. $)
    cardalephOLD $p |- ( ( om C_ A /\ ( card ` A ) = A ) ->
                  A = ( aleph ` |^| { x e. On | A C_ ( aleph ` x ) } ) ) $=
      ( vy com wss ccrd cfv wceq wa cv cale con0 crab cint c0 csuc wrex wlim
      wo wcel cardon eleq1 mpbii alephle fveq2 sseq2d rcla4ev mpdan onintrab2
      sylib word eloni w3o ordzsl 3orass bitri 3syl adantl wi ax-17 hbrab1
      hbint hbfv hbss onminsb a1i aleph0 syl6eq sseq1d biimprd anim12d eqss
      syl6ibr com12 ancoms wn visset sucid eleq2 mpbiri onnminsb syl5 imp csdm
      wbr alephsuc sylan9eqr eleq2d biimpd cdom breq2 cvv wb fvex domtri mpan2
      sylibrd carddom syl sseq1 alephcard sseq2i syl6bb sylibd sylan9r mtod
      exp32 r19.23adv onelon sylan adantld mpcom alephon onelssi nsyl nrexdv
      adantr ciun alephlim eliun mtbird ex jaod onsseleq mpbid ord sylsyld mpd
      ) DBEZBFGZBHZIZBAJZKGZEZALMZNZOHZYQCJZPZHZCLQZYQRZSZSZBYQKGZHZYKUUEYIYKBL
      TZYQLTZUUEYKYJLTUUHBUAYJBLUBUCZUUHYOALQZUUIUUHBBKGZEZUUKBUDYOUUMABLYMBHYN
      UULBYMBKUEUFUGUHZYOAUIUJZUUIYQUKZUUEYQULUUPYRUUBUUCUMUUECYQUNYRUUBUUCUOUP
      UJUQURYLYRUUGUUDYKYIYRUUGUSYRYKYIIZUUGYRUUQBUUFEZUUFBEZIUUGYRYKUURYIUUSYK
      UURUSYRYKUUHUUKUURUUJUUNYOUURAACBUUFYSBTAUTACYQKYSKTAUTACYPYOACLVAVBVCVDY
      MYQHYNUUFBYMYQKUEUFVEZUQVFYRUUSYIYRUUFDBYRUUFOKGDYQOKUEVGVHVIVJVKBUUFVLVM
      VNVOYKUUDUUGUSYIYKUUHUUDBUUFTZVPZUUGUUJYKUUBUVBUUCYKUUAUVBCLYKYSLTZUUAUVB
      YKUVCUUAIZIUVABYSKGZEZUVDUVFVPZYKUVCUUAUVGUUAYSYQTZUVCUVGUUAUVHYSYTTYSCVQ
      VRYQYTYSVSVTYOUVFAYSYMYSHYNUVEBYMYSKUEUFWAZWBWCURUVDUVABUVEYMWDWEZALMNZTZ
      YKUVFUVDUVAUVLUVDUUFUVKBUUAUVCUUFYTKGUVKYQYTKUEAYSWFWGWHWIYKUVLYJUVEFGZEZ
      UVFYKUUHUVLUVNUSUUJUUHUVLBUVEWJWEZUVNUUHUVLUVEBWDWEZVPZUVOUVJUVPABYMBUVEW
      DWKWAUUHUVEWLTZUVOUVQWMYSKWNZBUVELWLWOWPWQUUHUVRUVNUVOWMUVSBUVELWLWRWPWQW
      SYKUVNBUVMEUVFYJBUVMWTUVMUVEBYSXAXBXCXDXEXFXGXHYKUUHUUCUVBUSUUJUUHUUCUVBU
      UHUUCIZUVABUVETZCYQQZUUHUWBVPUUCUUHUWACYQUUHUVHIZUVFUWAUVCUWCUVGUUHUUIUVH
      UVCUUOYQYSXIXJUVCUVHUVGUUHUVIXKXLUVEBYSXMXNXOXPXQUVTUVABCYQUVEXRZTUWBUVTU
      UFUWDBUUHUUIUUCUUFUWDHUUOCYQLXSXJWHCBYQUVEXTXCYAYBWSYCUUHUVAUUGUUHUURUVAU
      UGSZUUHUUKUURUUNUUTWSUUHUUFLTUURUWEWMYQXMBUUFYDWPYEYFYGURYCYH $.
      $( [9-Nov-2003] $)
  $}

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function for nonzero
       arguments.  Theorem 64 of [Suppes] p. 229. $)
    alephval2 $p |- ( ( A e. On /\ (/) e. A ) -> ( aleph ` A ) =
                    |^| { x e. On | A. y e. A ( aleph ` y ) ~< x } ) $=
      ( vz con0 wcel c0 wa cale cfv cv csdm wbr wral crab wn cint wceq
      alephordi r19.21aiv alephon jctil breq2 ralbidv elrab sylibr adantr wb
      oneli ccrd cardsdomel alephcard eleq2i syl6rbb syl ibi com cdom wo wi
      cvv omex visset entri3 mp2an wss carddom cardom sseq1i bitr3i wrex
      cardidm cardalephex mpbii alephord ancoms cen cardid sdomen1 breq1
      syl5rbbr sylan9bb fveq2 breq1d rcla4v sdomirr sdomen2 syl5bbr mtbiri
      nsyli com12 adantl sylbird exp31 r19.23adv syl5 syl5bi wne ne0i onelon
      alephgeom ssdomg ax-mp sylbi domtr sylan2 domnsym expr r19.2z ex rexnal
      syl6ib expimpd a1d com3r jaod mpi simprbi con3i syl6 ssrab2 oneqmini
      syl11anc ) CEFZGCFZHZCIJZBKZIJZAKZLMZBCNZAEOZFZDKZYMFZPZDYGNZYGYMQRZYDYNY
      EYDYGEFZYIYGLMZBCNZHYNYDUUBYTYDUUABCYHCSTCUAZUBYLUUBAYGEYJYGRYKUUABCYJYGY
      ILUCUDUEUFUGYFYQDYGYFYOYGFZYIYOLMZBCNZPZYQUUDYOYGLMZYFUUGUUDUUHUUDYOEFZUU
      DUUHUHYGYOUUCUIUUIUUHYOYGUJJZFUUDYOYGUKUUJYGYOCULUMUNUOUPYFUQYOURMZYOUQUR
      MZUSZUUHUUGUTZUQVAFZYOVAFZUUMVBDVCZUQYOVAVAVDVEYFUUKUUNUULYDUUKUUNUTYEUUK
      UQYOUJJZVFZYDUUNUUKUQUJJZUURVFZUUSUUOUUPUVAUUKUHVBUUQUQYOVAVAVGVEUUTUQUUR
      VHVIVJUUSUURYJIJZRZAEVKZYDUUNUUSUURUJJUURRUVDYOVLAUURVMVNYDUVCUUNAEYDYJEF
      ZUVCUUNYDUVEHZUVCHUUHYJCFZUUGUVFUVGUVBYGLMZUVCUUHUVEYDUVGUVHUHYJCVOVPUUHU
      URYGLMZUVCUVHUUPUURYOVQMZUVIUUHUHUUQYOVRZUURYOYGVAVSVEUURUVBYGLVTWAWBUVCU
      VGUUGUTUVFUVGUVCUUGUVGUUFUVBYOLMZUVCUUEUVLBYJCYHYJRYIUVBYOLYHYJIWCWDWEUVC
      UVLUVBUVBLMZUVBWFUVLUVBUURLMZUVCUVMUUPUVJUVNUVLUHUUQUVKUURYOUVBVAWGVEUURU
      VBUVBLUCWHWIWJWKWLWMWNWOWPWQUGUULUUHYFUUGUULYFUUGUTUUHUULYDYEUUGYEUULYDHZ
      UUGYEUVOUUEPZBCVKZUUGYECGWRZUVOUVQUTCGWSUVOUVPBCNZUVRUVQUVOUVPBCUULYDYHCF
      ZUVPYDUVTHUULYHEFZUVPCYHWTUULUWAHYOYIURMZUVPUWAUULUQYIURMZUWBUWAUQYIVFZUW
      CYHXAUUOUWDUWCUTVBUQYIVAXBXCXDYOUQYIXEXFYOYIXGUOXFXHTUVRUVSUVQUVPBCXIXJWP
      UOUUEBCXKXLWKXMXNXOXPXQWPYPUUFYPUUIUUFYLUUFAYOEYJYORYKUUEBCYJYOYILUCUDUEX
      RXSXTTYMEVFYNYRHYSUTYLAEYADYGYMYBXCYC $.
      $( [15-Nov-2003] $)
  $}

  ${
    $d x y z w A $.
    dominfac.1 $e |- A e. _V $.
    $( A nonempty set that is a subset of its union is infinite.  This version
       is proved from ~ ax-ac .  See ~ dominf for a version proved from
       ~ ax-cc . $)
    dominfac $p |- ( ( A =/= (/) /\ A C_ U. A ) -> om ~<_ A ) $=
      ( vx vz vw vy cv c0 wne cuni wss wa com cdom wbr wi wceq neeq1 id unieq
      sseq12d anbi12d breq2 imbi12d cpw cin crab copab crdg cres wf1 eqid
      inf3lem6 cvv wcel visset pwex f1dom2g ax-mp csdm wn cfn pwfi biimpi
      isfinite 3imtr4i con3i wb omex domtri mp2an 3syl vtocl ) CGZHIZVNVNJZKZLZ
      MVNNOZPAHIZAAJZKZLZMANOZPCABVNAQZVRWCVSWDWEVOVTVQWBVNAHRWEVNAVPWAWESVNATU
      AUBVNAMNUCUDVRMVNUEZDGEGVNUFFGKEVNUGQFDUHZHUIMUJZUKZMWFNOZVSCFDEAAWHWGWGU
      LWHULBBUMWFUNUOZWIWJPVNCUPZUQZMWFUNWHURUSWFMUTOZVAZVNMUTOZVAZWJVSWPWNVNVB
      UOZWFVBUOZWPWNWRWSVNVCVDVNVEWFVEVFVGMUNUOZWKWJWOVHVIWMMWFUNUNVJVKWTVNUNUO
      VSWQVHVIWLMVNUNUNVJVKVFVLVM $.
      $( [25-Mar-2007] $)
  $}

