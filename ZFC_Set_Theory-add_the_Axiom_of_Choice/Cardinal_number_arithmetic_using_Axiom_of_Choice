$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal number arithmetic using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    infunabs.1 $e |- A e. _V $.
    infunabs.2 $e |- B e. _V $.
    $( An infinite set is equinumerous to its union with a smaller one. $)
    infunabs $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A u. B ) ~~ A ) $=
      ( com cdom wbr wa cun cen ccda co cxp c2o wcel domtr domentr sbth sylancl
      cvv uncdadom cdadom2 xp2cda syl6breqr wn 2onn omsdomnn simpli mpan xpdom2
      ax-mp syl2an infxpidm adantl syl11anc cdadom3 ancoms sylancr ssun1 ssdomg
      syl wss mp2 ) EAFGZBAFGZHZABIZAFGZAVGFGZVGAJGVFVGABKLZFGVJAJGZVHABCDUAVEV
      DVKVEVDHZVJAFGZAVJFGVKVLVJAAMZFGZVNAJGZVMVEVJANMZFGVQVNFGZVOVDVEVJAAKLVQF
      BAADCCUBACUCUDVDNAFGZVRNEFGZVDVSVTENJGUEZNEOVTWAHUFNUGUKUHNEAPUINAACCUJVA
      VJVQVNPULVDVPVEACUMUNVJVNAQUOABCDUPVJARSUQVGVJAQURATOAVGVBVICABUSAVGTUTVC
      VGARS $.
      $( [28-Sep-2004] $)

    $( Absorption law for addition to an infinite cardinal. $)
    infcdaabs $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A +c B ) ~~ A ) $=
      ( cdom wbr com ccda co cen wa cxp c2o cdadom2 xp2cda syl6breqr wcel domtr
      wn 2onn omsdomnn ax-mp simpli mpan xpdom2 syl2an infxpidm adantl syl11anc
      syl domentr cdadom3 sbth sylancl ancoms ) BAEFZGAEFZABHIZAJFZUPUQKZURAEFZ
      AUREFUSUTURAALZEFZVBAJFZVAUPURAMLZEFVEVBEFZVCUQUPURAAHIVEEBAADCCNACOPUQMA
      EFZVFMGEFZUQVGVHGMJFSZMGQVHVIKTMUAUBUCMGARUDMAACCUEUJURVEVBRUFUQVDUPACUGU
      HURVBAUKUIABCDULURAUMUNUO $.
      $( [30-Sep-2004] $)

    $( The sum of two cardinal numbers is their maximum, if one of them is
       infinite.  Proposition 10.41 of [TakeutiZaring] p. 95. $)
    infcda $p |- ( om ~<_ A -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( com cdom wbr ccda co cun cen cvv wcel infcdaabs infunabs ensym syl entr
      wa syl11anc wo mp2an ex domtr cdacomen sylancr uncom syl6breq sylan exp31
      entri3 pm2.43d jaod mpi ) EAFGZBAFGZABFGZUAZABHIZABJZKGZBLMALMURDCBALLUKU
      BUOUPVAUQUOUPVAUOUPSZUSAKGAUTKGZVAABCDNVBUTAKGVCABCDOUTACPQUSAUTRTUCUOUQV
      AUOUQUQVAUOUQSEBFGZUQVAEABUDVDUQSZUSBAJZUTKVEUSBAHIZKGVGVFKGZUSVFKGABCDUE
      VEVGBKGBVFKGZVHBADCNVEVFBKGVIBADCOVFBDPQVGBVFRTUSVGVFRUFBAUGUHUIUJULUMUN
      $.
      $( [28-Sep-2004] $)

    $( The cardinality of an infinite set does not change after subtracting a
       strictly smaller one.  Example in [Enderton] p. 164. $)
    infdif $p |- ( ( om ~<_ A /\ B ~< A ) -> ( A \ B ) ~~ A ) $=
      ( com cdom wbr csdm wa cen cvv wcel ccda co wn mp2an wb domtri ax-mp ex
      cdif wss difss ssdom2g mp2 cun sdomdom infunabs syl sylan2 wo omex entri2
      ensym ssun1 ssdomg unex mpbi undif1 difexg uncdadom cdadom1 domtr sylancr
      syl5eqbrr domrefg infcdaabs mpan2 ensdomtr ancoms domsdomtr syl2an expcom
      sylan mtoi adantl simpll endomtr cdafi anidms an32s con3d syl5bi mpd jaod
      wi mpi con2bii sylibr eqbrtrri cdadom2 3syl cdainf sylancl domentr syldan
      syl11anc sbth ) EAFGZBAHGZIZABUAZAFGZAXBFGZXBAJGAKLZXBAUBXCCABUCXBAKUDUEW
      SWTAXBXBMNZFGZXDXAAABUFZJGZXHXFFGZXGWTWSBAFGZXIBAUGWSXKIXHAJGXIABCDUHXHAC
      UNUIUJZXABXBHGZBXBFGZXJXAXBBFGZOZXMXAEBFGZBEHGZUKZXPEKLZBKLZXSULDEBKKUMPX
      AXQXPXRWTXQXPWFWSWTXQXPWTXQIZXOXHAHGZAXHFGZYCOZXEAXHUBYDCABUOAXHKUPUEXEXH
      KLYDYEQCABCDUQAXHKKRPURXOYBYCXOXHBBMNZFGZYFAHGZYCYBXOXHXBBUFZYFFABUSZXOYI
      XBBMNZFGYKYFFGYIYFFGXBBXEXBKLZCABKUTSZDVAZXBBBYMDDVBYIYKYFVCVDVEZXQWTYHXQ
      YFBJGZWTYHXQBBFGZYPYAYQDBKVFSBBDDVGVHYFBAVIVNVJXHYFAVKVLVMVOTVPXAXRXPXAXR
      IZWSXPWSWTXRVQWSAEHGZOZYRXPXTXEWSYTQULCEAKKRPYRXOYSYRXOYSXAXOXRYSXAXOIAYF
      FGZYFEHGZYSXRXAXIYGUUAXOXLYOAXHYFVRVLXRUUBBBVSVTAYFEVKVLWATWBWCWDTWEWGXOX
      MYLYAXOXMOQYMDXBBKKRPWHWIBXBUGXNXHYKFGYKXFFGXJYIXHYKFYJYNWJBXBXBDYMYMWKXH
      YKXFVCVDWLAXHXFVRWQWSXGIZXFXBJGZXDUUCEXBFGZXBXBFGZUUDUUCEXFFGUUEEAXFVCXBY
      MWMWIYLUUFYMXBKVFSXBXBYMYMVGWNXGUUDXDWFWSXGUUDXDAXFXBWOTVPWDWPXBAWRVD $.
      $( [22-Oct-2004] $)

    $( Cardinality ordering for an infinite set difference. $)
    infdif2 $p |- ( om ~<_ A -> ( ( A \ B ) ~<_ B <-> A ~<_ B ) ) $=
      ( com cdom wbr cdif csdm wn wa cen wi cvv wcel ax-mp ex wb domtri mp2an
      infdif ensym syl difexg sdomentr adantl mpd con3d 3imtr4g wss ssdom2g mp2
      difss domtr mpan impbid1 ) EAFGZABHZBFGZABFGZUQBURIGZJZBAIGZJZUSUTUQVCVAU
      QVCVAUQVCKZAURLGZVAVEURALGVFABCDUAURACUBUCVCVFVAMUQVCVFVAURNOZVCVFKVAMANO
      ZVGCABNUDPZBAURNUEPQUFUGQUHVGBNOZUSVBRVIDURBNNSTVHVJUTVDRCDABNNSTUIURAFGZ
      UTUSVHURAUJVKCABUMURANUKULURABUNUOUP $.
      $( [24-Mar-2007] $)

    $( Absorption law for multiplication with an infinite cardinal. $)
    infxpabs $p |- ( ( om ~<_ A /\ B =/= (/) /\ B ~<_ A ) ->
                     ( A X. B ) ~~ A ) $=
      ( com cdom wbr c0 wne cxp wa xpdom2 infxpidm domentr syl2an ancoms xpdom3
      cen sbth 3impa 3com23 ) EAFGZBAFGZBHIZABJZARGZUBUCUDUFUBUCKUEAFGZAUEFGUFU
      DUCUBUGUCUEAAJZFGUHARGUGUBBAACCLACMUEUHANOPABCQUEASOTUA $.
      $( [29-Nov-2006] $) $( [30-Sep-2004] $)

    $( Dominance law for multiplication with an infinite cardinal. $)
    infxpdom $p |- ( ( om ~<_ A /\ B ~<_ A ) -> ( A X. B ) ~<_ A ) $=
      ( com cdom wbr wa cxp wceq xpeq2 breq1d wne w3a cen infxpabs 3com23 endom
      c0 syl 3expa xp0 0dom eqbrtri a1i pm2.61ne ) EAFGZBAFGZHZABIZAFGZASIZAFGZ
      BSBSJUJULAFBSAKLUGUHBSMZUKUGUHUNNUJAOGZUKUGUNUHUOABCDPQUJARTUAUMUIULSAFAU
      BAUCUDUEUF $.
      $( [26-Mar-2006] $)

    $( Absorption law for multiplication with an infinite cardinal.  Equivalent
       to Proposition 10.41 of [TakeutiZaring] p. 95. $)
    infxp $p |- ( ( om ~<_ A /\ B =/= (/) ) ->
                   ( A X. B ) ~~ ( A u. B ) ) $=
      ( com cdom wbr c0 wne wa cxp cun cen cvv wcel infxpabs infunabs ensym syl
      entr wo entri3 mp2an 3expa adantlr syl11anc ex wi domtr infn0 adantr 3jca
      w3a simpr 3adant2 xpcomen mpan 3syl uncom syl6breq jaod mpi ) EAFGZBHIZJZ
      BAFGZABFGZUAZABKZABLZMGZBNOANOVHDCBANNUBUCVEVFVKVGVEVFVKVEVFJVIAMGZAVJMGZ
      VKVCVDVFVLABCDPUDVCVFVMVDVCVFJVJAMGVMABCDQVJACRSUEVIAVJTUFUGVCVGVKUHVDVCV
      GVKVCVGJZVIBALZVJMVNEBFGZAHIZVGUMZBAKZVOMGZVIVOMGZVNVPVQVGEABUIVCVQVGACUJ
      UKVCVGUNULVRVSBMGBVOMGZVTBADCPVPVGWBVQVPVGJVOBMGWBBADCQVOBDRSUOVSBVOTUFVI
      VSMGVTWAABCDUPVIVSVOTUQURBAUSUTUGUKVAVB $.
      $( [1-Dec-2006] $) $( [28-Sep-2004] $)

    $( An exponentiation law for infinite cardinals.  Exercise 34 of [Enderton]
       p. 165. $)
    infmap1 $p |- ( ( ( 2o ~<_ A /\ om ~<_ B ) /\ A ~<_ B ) ->
                  ( A ^m B ) ~~ ( 2o ^m B ) ) $=
      ( cdom wbr com c2o cmap co cen wa mapdom1 cxp infxpidm mpan ax-mp domentr
      mp2an syl2an con0 2on elisseti enref mapen cpw canth2 sdomdom pw2en oprex
      csdm mapxpen 3syl domtr sbth an31s ) ABEFZGBEFZHAEFZABIJZHBIJZKFZUQURLUTV
      AEFZVAUTEFVBUSUQUTBBIJZEFVDVAEFZVCURABBDDMURBBNZBKFZHVFIJZVAKFZVEBDOHHKFV
      GVIHHUAUBUCZUDHHVFBVJDUEPVDVHEFZVIVEVDVABIJZEFZVLVHKFVKBVAEFZVMBBUFZEFZVO
      VAKFVNBVOUKFVPBDUGBVOUHQBDUIBVOVARSBVABHBIUJDMQHBBVJDDULVDVLVHRSVDVHVARPU
      MUTVDVAUNTHABCDMUTVAUOTUP $.
      $( [30-Sep-2004] $)
  $}

  ${
    $d x y A $.
    infpss.1 $e |- A e. _V $.
    $( Every infinite set has an equinumerous proper subset.  Exercise 7 of
       [TakeutiZaring] p. 91. $)
    infpss $p |- ( om ~<_ A -> E. x ( x C. A /\ x ~~ A ) ) $=
      ( vy cv wcel wex com cdom wbr wpss cen wa c0 wne wceq sylibr csdm c1o cvv
      infn0 n0 sylib wi csn cdif cin wn snidg ancli elin n0i syl con2bii visset
      disj4 ensn1 1onn infsdomnn mpan2 ensdomtr sylancr snex infdif mpdan ax-mp
      difexg psseq1 breq1 anbi12d cla4ev syl2an ex exlimiv mpcom ) DEZBFZDGZHBI
      JZAEZBKZVTBLJZMZAGZVSBNOVRBCUADBUBUCVQVSWDUDDVQVSWDVQBVPUEZUFZBKZWFBLJZWD
      VSVQBWEUGZNPZUHZWGVQVPWIFZWKVQVQVPWEFZMWLVQWMVPBUIUJVPBWEUKQWIVPULUMWJWGB
      WEUPUNQVSWEBRJZWHVSWESLJSBRJZWNVPDUOUQVSSHFWOURBSCUSUTWESBVAVBBWECVPVCVDV
      EWCWGWHMAWFBTFWFTFCBWETVGVFVTWFPWAWGWBWHVTWFBVHVTWFBLVIVJVKVLVMVNVO $.
      $( [17-Jul-2008] $) $( [23-Oct-2004] $)
  $}

  ${
    $d x A $.
    iunctb.1 $e |- A e. _V $.
    iunctb.2 $e |- B e. _V $.
    $( The countable union of countable sets is countable (indexed union
       version of ~ unictb ). $)
    iunctb $p |- ( ( A ~<_ om /\ A. x e. A B ~<_ om ) ->
                 U_ x e. A B ~<_ om ) $=
      ( com cdom wbr wral ciun cxp omex iundom wcel domrefg ax-mp infxpdom mpan
      cvv cen wb xpex xpcomen domen1 mp2an sylib domtr syl2an ancoms ) CFGHABIZ
      BFGHZABCJZFGHZUJULBFKZGHUNFGHZUMUKABFCDLEMUKFBKZFGHZUOFFGHZUKUQFSNURLFSOP
      FBLDQRUNSNUPUNTHUQUOUABFDLUBFBLDUCUPUNFSUDUEUFULUNFUGUHUI $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.
    unictb.1 $e |- A e. _V $.
    $( The countable union of countable sets is countable.  Theorem 6Q of
       [Enderton] p. 159.  See ~ iunctb for indexed union version. $)
    unictb $p |- ( ( A ~<_ om /\ A. x e. A x ~<_ om ) -> U. A ~<_ om ) $=
      ( com cdom wbr cv wral wa cuni ciun uniiun visset iunctb syl5eqbr ) BDEFA
      GZDEFABHIBJABPKDEABLABPCAMNO $.
      $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The union of two countable sets is countable.  (Contributed by FL,
       25-Aug-2006.) $)
    unctb $p |- ( ( A ~<_ om /\ B ~<_ om ) -> ( A u. B ) ~<_ om ) $=
      ( vx com cdom wbr wa cpr cuni cun wcel wceq reldom brrelexi uniprg syl2an
      cvv cv breq1 biimprcd wral cen wn cfn prfi isfinite1 ax-mp simpli wo elpr
      visset jaao syl5bi ralrimiv prex unictb sylancr eqbrtrrd ) ADEFZBDEFZGZAB
      HZIZABJZDEUSAQKBQKVCVDLUTADEMNBDEMNABQQOPVAVBDEFZCRZDEFZCVBUAVCDEFVEDVBUB
      FUCZVBUDKVEVHGABUEVBUFUGUHVAVGCVBVFVBKVFALZVFBLZUIVAVGVFABCUKUJUSVIVGUTVJ
      VIVGUSVFADESTVJVGUTVFBDESTULUMUNCVBABUOUPUQUR $.
      $( [16-Sep-2008] $) $( [25-Aug-2006] $)
  $}

  ${
    $d x y z w v u f A $.  $d x y z w f v u B $.  $d u v f R $.
    infmap2lem.1 $e |- A e. _V $.
    infmap2lem.2 $e |- B e. _V $.
    infmap2lem.3 $e |- R = { <. z , w >. | ( ( z C_ A /\ z ~~ B ) /\
                        w : B -onto-> z ) } $.
    $( Lemma for ~ infmap2 .  Technical result that is used several times. $)
    infmap2lem1 $p |- ( ( f C_ R /\ f Fn dom R ) ->
                       ( v e. { x | ( x C_ A /\ x ~~ B ) } ->
                       ( v C_ A /\ ( f ` v ) : B -onto-> v ) ) ) $=
      ( cv wss cen wbr wa cab wcel wfo wex cdm wfn cfv copab dmeqi dmopab anass
      19.42v ensym wf1o visset bren f1ofo eximi sylbi syl pm4.71i 3bitr4i abbii
      anbi2i 3eqtri wceq sseq1 breq1 anbi12d cbvabv eleq2i cop fnopfv ssel fvex
      eqtri foeq3 foeq1 anbi2d opelopab bitri simpl anim1i expdimp syl5bir
      syl56 ) DLZALZEMZWDFNOZPZAQZRWCGUAZRZHLZGMZWKWIUBZPWCEMZFWCWCWKUCZSZPZWIW
      HWCWIBLZEMZWRFNOZPZBQZWHWIXAFWRCLZSZPZBCUDZUAXECTZBQXBGXFKUEXEBCUFXGXABXA
      XDCTZPWSWTXHPZPXGXAWSWTXHUGXAXDCUHWTXIWSWTXHWTFWRNOZXHWRFJUIXJFWRXCUJZCTX
      HFWRCBUKULXKXDCFWRXCUMUNUOUPUQUTURUSVAXAWGBAWRWDVBWSWEWTWFWRWDEVCWRWDFNVD
      VEVFVLVGWLWMWJWQWMWJPWCWOVHZWKRWLXLGRZWQWIWCWKVIWKGXLVJXMWNWCFNOZPZWPPZWQ
      XMXLXFRXPGXFXLKVGXEXOFWCXCSZPXPBCWCWODUKWCWKVKWRWCVBZXAXOXDXQXRWSWNWTXNWR
      WCEVCWRWCFNVDVEWRWCFXCVMVEXCWOVBXQWPXOFWCXCWOVNVOVPVQXOWNWPWNXNVRVSUOWBVT
      WA $.
      $( [1-Oct-2004] $)

    $( Lemma for ~ infmap2 .  Given the relation ` R ` , we use the Axiom of
       Choice ~ ac7g to extract a function ` f ` that provides the one-to-one
       mapping for the dominance relation. $)
    infmap2lem2 $p |- { x | ( x C_ A /\ x ~~ B ) } ~<_ ( A ^m B ) $=
      ( vf vv vu cv wss wa wbr cvv wcel wfo syl6 cdm wfn wex cen cab cmap copab
      cdom cpw cxp df-xp pwex oprex xpex eqeltrri simpl visset elpw sylibr fofn
      co crn adantl forn sseq1d biimparc elmap df-f bitri sylanbrc jca ssopab2i
      wf adantlr ssexi eqeltri ax-mp df-pw ss2abi cfv infmap2lem1 fof fss sylan
      ac7g ancoms wceq wb simpr anim12d rneq eqeqan12d syl5ib fveq2 impbid1 mpi
      dom2d exlimiv ) JMZFNWSFUAUBOZJUCZAMZDNZXBEUDPZOZAUEZDEUFVAZUHPZFQRXAFBMZ
      DNZXIEUDPZOEXICMZSZOZBCUGZQIXOXIDUIZRZXLXGRZOZBCUGZXPXGUJXTQBCXPXGUKXPXGD
      GULZDEUFUMUNUOXNXSBCXJXMXSXKXJXMOZXQXRYBXJXQXJXMUPXIDBUQURUSYBXLEUBZXLVBZ
      DNZXRXMYCXJEXIXLUTVCXMYEXJXMYDXIDEXIXLVDVEVFXREDXLVMYCYEODEXLGHVGEDXLVHVI
      VJVKVNVLVOVPQFJWEVQWTXHJWTXFQRXHXFXCAUEZXPYFQADVRYAUOXEXCAXCXDUPVSVOWTKLX
      FXGKMZWSVTZLMZWSVTZQWTYGXFRZYGDNZEYGYHSZOZYHXGRZABCKDEFJGHIWAZYNEDYHVMZYO
      YMYLYQYMEYGYHVMYLYQEYGYHWBEYGDYHWCWDWFDEYHGHVGUSTWTYKYIXFRZOYMEYIYJSZOZYH
      YJWGZYGYIWGZWHWTYKYMYRYSWTYKYNYMYPYLYMWITWTYRYIDNZYSOYSABCLDEFJGHIWAUUCYS
      WITWJYTUUAUUBUUAYHVBZYJVBZWGYTUUBYHYJWKYMYSUUDYGUUEYIEYGYHVDEYIYJVDWLWMYG
      YIWSWNWOTWQWPWRVQ $.
      $( [30-Sep-2004] $)
  $}

  ${
    $d x z w A $.  $d x z w B $.
    infmap2.1 $e |- A e. _V $.
    infmap2.2 $e |- B e. _V $.
    $( An exponentiation law for infinite cardinals.  Similar to Lemma 6.2 of
       [Jech] p. 43.  We start with ~ infmap2lem2 and also prove the other
       direction of the dominance relation.  We obtain equinumerosity with
       Schroeder-Bernstein ~ sbth and finally eliminate the degenerate case
       ` B = (/) ` . $)
    infmap2 $p |- ( ( om ~<_ A /\ B ~<_ A ) ->
                      ( A ^m B ) ~~ { x | ( x C_ A /\ x ~~ B ) } ) $=
      ( vz vw com cdom wbr wa cmap co cv wss cen cab c0 wceq c1o opreq2 breq12d
      breq2 anbi2d abbidv wne xpcomen infxpabs 3com23 3expa entr sylancr ssenen
      cxp xpex cvv wcel oprex abid2 wf elmap fssxp cdm wfun ffun visset fundmen
      ensym 3syl fdm breqtrd jca sylbi ss2abi eqsstr3i ssdomg mp2 domentr copab
      mpan wfo eqid infmap2lem2 sbth sylancl elisseti enref map0e csn df-sn en0
      1onn df1o2 anbi2i 0ss sseq1 mpbiri pm4.71ri bitr4i abbii 3eqtr4ri 3brtr4i
      a1i pm2.61ne ) HBIJZCBIJZKZBCLMZANZBOZXICPJZKZAQZPJZBRLMZXJXIRPJZKZAQZPJZ
      CRCRSZXHXOXMXRPCRBLUAXTXLXQAXTXKXPXJCRXIPUCUDUEUBXGCRUFZKZXHXMIJZXMXHIJXN
      YBCBUNZBPJZXIYDOZXKKZAQZXMPJZYCYBYDBCUNZPJYJBPJZYECBEDUGXEXFYAYKXEYAXFYKB
      CDEUHUIUJYDYJBUKULAYDBCCBEDUODUMXHYHIJZYIYCXHUPUQXHYHOYLBCLURXHXIXHUQZAQY
      HAXHUSYMYGAYMCBXIUTZYGBCXIDEVAYNYFXKCBXIVBYNXIXIVCZCPYNXIVDYOXIPJXIYOPJCB
      XIVEXIAVFZVGYOXIYPVHVICBXIVJVKVLVMVNVOXHYHUPVPVQXHYHXMVRVTVIAFGBCFNZBOYQC
      PJKCYQGNWAKFGVSZDEYRWBWCXHXMWDWEXSXGTTXOXRPTTHWLWFWGBDWHRWIXIRSZAQTXRARWJ
      WMXQYSAXQXJYSKYSXPYSXJXIWKWNYSXJYSXJRBOBWOXIRBWPWQWRWSWTXAXBXCXD $.
      $( [1-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The sum of two alephs is their maximum.  Equation 6.1 of [Jech]
       p. 42. $)
    alephadd $p |- ( ( aleph ` A ) +c ( aleph ` B ) ) ~~
                   ( ( aleph ` A ) u. ( aleph ` B ) ) $=
      ( con0 wcel cale cfv ccda co cun cen wbr wn cvv wceq ax-mp eleq2i cxp com
      wa c0 oprex cdm wfn alephfnon fndm notbii csn c1o 0ex cdavali xpundi xp0r
      3eqtr2i ndmfv opreqan12d adantr adantl uneq12d un0 3eqtr4a syl2anbr eqeng
      syl6eq mpsyl ex wss alephgeom cdom fvex ssdom2g infcda syl sylbi cdacomen
      wi entr sylancr uncom syl6breq pm2.61ii ) ACDZBCDZAEFZBEFZGHZWCWDIZJKZWAL
      ZWBLZWGWEMDWHWISWEWFNZWGWCWDGUAWHAEUBZDZLZBWKDZLZWJWIWLWAWKCAECUCWKCNUDCE
      UEOZPUFWNWBWKCBWPPUFWMWOSZTTGHZTWEWFWRTTUGZQTUHUGZQITWSWTIZQTTTUIUIUJTWSW
      TUKXAULUMWMWOWCTWDTGAEUNZBEUNZUOWQWFTTITWQWCTWDTWMWCTNWOXBUPWOWDTNWMXCUQU
      RTUSVCUTVAWEWFMVBVDVEWARWCVFZWGAVGXDRWCVHKZWGWCMDXDXEVOAEVIZRWCMVJOWCWDXF
      BEVIZVKVLVMWBRWDVFZWGBVGXHRWDVHKZWGWDMDXHXIVOXGRWDMVJOXIWEWDWCIZWFJXIWEWD
      WCGHZJKXKXJJKWEXJJKWCWDXFXGVNWDWCXGXFVKWEXKXJVPVQWDWCVRVSVLVMVT $.
      $( [29-Sep-2004] $)

    $( The product of two alephs is their maximum.  Equation 6.1 of [Jech]
       p. 42. $)
    alephmul $p |- ( ( A e. On /\ B e. On ) ->
  ( ( aleph ` A ) X. ( aleph ` B ) ) ~~ ( ( aleph ` A ) u. ( aleph ` B ) ) ) $=
      ( con0 wcel com cale cfv cdom wbr c0 wne cxp wss alephgeom cvv wi ssdom2g
      fvex ax-mp sylbi cun cen infn0 syl infxp syl2an ) ACDZEAFGZHIZBFGZJKZUHUJ
      LUHUJUAUBIBCDZUGEUHMZUIANUHODUMUIPAFRZEUHOQSTULEUJMZUKBNUOEUJHIZUKUJODUOU
      PPBFRZEUJOQSUJUQUCUDTUHUJUNUQUEUF $.
      $( [16-Jun-2007] $) $( [29-Sep-2004] $)

    $( An exponentiation law for alephs.  Lemma 6.1 of [Jech] p. 42. $)
    alephexp1 $p |- ( ( ( A e. On /\ B e. On ) /\ A C_ B ) ->
  ( ( aleph ` A ) ^m ( aleph ` B ) ) ~~ ( 2o ^m ( aleph ` B ) ) ) $=
      ( con0 wcel wa wss c2o cale cfv cdom wbr com cmap co ax-mp alephgeom fvex
      cvv wi ssdom2g cen csdm nnsdom sdomdom sylbi domtr sylancr anim12i adantr
      2onn alephord3 ssdomg syl6bi imp infmap1 syl11anc ) ACDZBCDZEZABFZEGAHIZJ
      KZLBHIZJKZEZVAVCJKZVAVCMNGVCMNUAKUSVEUTUQVBURVDUQGLJKZLVAJKZVBGLUBKZVGGLD
      VIUJGUCOGLUDOUQLVAFZVHAPVARDZVJVHSAHQZLVARTOUEGLVAUFUGURLVCFZVDBPVCRDVMVD
      SBHQZLVCRTOUEUHUIUSUTVFUSUTVAVCFZVFABUKVKVOVFSVLVAVCRULOUMUNVAVCVLVNUOUP
      $.
      $( [29-Sep-2004] $)
  $}

  ${
    $d x A $.
    $( An alternate representation of a successor aleph.  Compare ~ alephsuc
       and ~ alephsuc2 .  Equality can be obtained by taking the ` card ` of
       the right-hand side then using ~ alephcard and ~ carden . $)
    alephsuc3 $p |- ( A e. On ->
               ( aleph ` suc A ) ~~ { x e. On | x ~~ ( aleph ` A ) } ) $=
      ( con0 wcel cv cale cfv cen wbr crab csuc cdif cdom alephsuc2 difeq1d a1i
      csdm com cvv fvex wceq ccrd alephcard cardval2 eqtr3i difeq2d eqtrd wn wa
      difrab wb bren2 rabbiia eqtr4i syl6req sucelon alephgeom bitri wi ssdom2g
      wss ax-mp sylbi alephordilem1 infdif syl11anc eqbrtrd ensym syl ) BCDZAEZ
      BFGZHIZACJZBKZFGZHIVPVNHIVJVNVPVLLZVPHVJVQVKVLMIZACJZVKVLQIZACJZLZVNVJVQV
      SVLLWBVJVPVSVLABNOVJVLWAVSVLWAUAVJVLUBGVLWABUCAVLUDUEPUFUGWBVRVTUHUIZACJV
      NVRVTACUJVMWCACVMWCUKVKCDVKVLULPUMUNUOVJRVPMIZVLVPQIVQVPHIVJRVPVAZWDVJVOC
      DWEBUPVOUQURVPSDWEWDUSVOFTZRVPSUTVBVCBVDVPVLWFBFTVEVFVGVNVPWFVHVI $.
      $( [23-Oct-2004] $)

    $( An expression equinumerous to 2 to an aleph power.  The proof equates
       the two laws for cardinal exponentiation ~ alephexp1 (which works if the
       base is less than or equal to the exponent) and ~ infmap2 (which works
       if the exponent is less than or equal to the base).  They can be equated
       only when the base is equal to the exponent, and this is the result. $)
    alephexp2 $p |- ( A e. On -> ( 2o ^m ( aleph ` A ) ) ~~
          { x | ( x C_ ( aleph ` A ) /\ x ~~ ( aleph ` A ) ) } ) $=
      ( con0 wcel cale cfv cmap co cv wss cen wbr wa cab c2o com cdom alephgeom
      cvv ax-mp wi ssdom2g sylbi domrefg infmap2 sylancl wb pm3.2 pm2.43i jctir
      fvex ssid alephexp1 oprex enen1 mpan 3syl mpbid ) BCDZBEFZUTGHZAIZUTJVBUT
      KLMANZKLZOUTGHZVCKLZUSPUTQLZUTUTQLZVDUSPUTJZVGBRUTSDZVIVGUABEUKZPUTSUBTUC
      VJVHVKUTSUDTAUTUTVKVKUEUFUSUSUSMZBBJZMVAVEKLZVDVFUGZUSVLVMUSVLUSUSUHUIBUL
      UJBBUMVESDVNVOOUTGUNVAVEVCSUOUPUQUR $.
      $( [23-Oct-2004] $)
  $}


