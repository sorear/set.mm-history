$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Grothendieck's universes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Univ $. $( The class of all Grothendieck's universes. $)

  $( Extend class notation to include the class of all Grothendieck's
     universes. $)
  cgru $a class Univ $.

  ${
    $d u x y $.
    $( A Grothendieck's universe is a set that is closed with respect to all
       the operations that are common in set theory: pairs, powersets, unions,
       intersections, cross products etc.  Grothendieck and alii,
       S&eacute;minaire de G&eacute;om&eacute;trie Alg&eacute;brique 4,
       Expos&eacute; I, p. 185.  It was designed to give a precise meaning to
       the concepts of categories of sets, groups... $)
    df-gruNEW $a |- Univ = { u | ( Tr u /\ A. x e. u ( ~P x e. u /\
      A. y e. u { x , y } e. u /\ A. y e. ( u ^m x ) U. ran y e. u ) ) } $.
  $}

  ${
    $d U u x y $.
    $( Properties of a Grothendieck's universe. $)
    elgrug $p |- ( U e. V ->
      ( U e. Univ <-> ( Tr U /\ A. x e. U ( ~P x e. U /\
        A. y e. U { x , y } e. U /\ A. y e. ( U ^m x ) U. ran y e. U ) ) ) ) $=
      ( vu cgru wcel cv wtr cpw cpr wral crn cuni cmap co w3a wa cab df-gruNEW
      eleq2i wceq treq eleq2 raleqbi1dv opreq1 raleqdv ralbidv bitrd 3anbi123d
      anbi12d elabg syl5bb ) CFGCEHZIZAHZJZUNGZUPBHZKZUNGZBUNLZUSMNZUNGZBUNUPOP
      ZLZQZAUNLZRZESZGCDGCIZUQCGZUTCGZBCLZVCCGZBCUPOPZLZQZACLZRZFVJCABETUAVIVTE
      CDUNCUBZUOVKVHVSUNCUCVGVRAUNCWAURVLVBVNVFVQUNCUQUDVAVMBUNCUNCUTUDUEWAVFVD
      BVPLVQWAVDBVEVPUNCUPOUFUGWAVDVOBVPUNCVCUDUHUIUJUEUKULUM $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d U x y z $.  $d A u x y z $.  $d B y $.  $d F x y z $.
    $( A Grothendieck's universe is transitive, so each element is a subset of
       the universe.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grutr $p |- ( ( U e. Univ /\ A e. U ) -> A C_ U ) $=
      ( vx vy cgru wcel wtr wss cv cpw cpr wral crn cuni cmap co w3a wa elgrug
      ibi simpld trss imp sylan ) BEFZBGZABFZABHZUEUFCIZJBFUIDIZKBFDBLUJMNBFDBU
      IOPLQCBLZUEUFUKRCDBESTUAUFUGUHBAUBUCUD $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the powerset of each of its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grupw $p |- ( ( U e. Univ /\ A e. U ) -> ~P A e. U ) $=
      ( vy vx cgru wcel cpw cv cpr wral crn cuni cmap co w3a wi wtr wa elgrug
      ibi simprd simp1 ralimi wceq pweq eleq1d rcla4cv 3syl imp ) BEFZABFZAGZBF
      ZUJCHZGZBFZUNDHZIBFDBJZUQKLBFDBUNMNJZOZCBJZUPCBJUKUMPUJBQZVAUJVBVARCDBEST
      UAUTUPCBUPURUSUBUCUPUMCABUNAUDUOULBUNAUEUFUGUHUI $.
      $( [9-Jun-2013] $)

    $( Any subset of an element of a Grothendieck's universe is also an
       element.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruss $p |- ( ( U e. Univ /\ A e. U /\ B C_ A ) -> B e. U ) $=
      ( cgru wcel wss wa cpw wb elpw2g adantl grupw grutr syldan sseld sylbird
      3impia ) CDEZACEZBAFZBCEZRSGZTBAHZEZUASUDTIRBACJKUBUCCBRSUCCEUCCFACLUCCMN
      OPQ $.
      $( [9-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains pairs derived from its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      grupr $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> { A , B } e. U ) $=
        ( vx vy cgru wcel cpr cv wral wi cpw crn cuni cmap co w3a wtr wa
        elgrug ibi simprd wceq preq2 eleq1d rcla4cv 3ad2ant2 com12 ralimdv
        syl5com preq1 syl6 com23 3imp ) CFGZACGZBCGZABHZCGZUOUQUPUSUOUQDIZBHZCG
        ZDCJZUPUSKUOUTLCGZUTEIZHZCGZECJZVEMNCGECUTOPJZQZDCJZUQVCUOCRZVKUOVLVKSD
        ECFTUAUBUQVJVBDCVJUQVBVHVDUQVBKVIVGVBEBCVEBUCVFVACVEBUTUDUEUFUGUHUIUJVB
        USDACUTAUCVAURCUTABUKUEUFULUMUN $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> U. ran F e. U ) $=
      ( vx vy cgru wcel wf crn cuni wa cmap co elmapg wi cv cpw cpr wral w3a
      wtr elgrug ibi simprd wceq rneq unieqd eleq1d rcla4cv 3ad2ant3 ralimi
      opreq2 eleq2d imbi1d 3syl imp sylbird 3impia ) BFGZABGZABCHZCIZJZBGZUSUTK
      VACBALMZGZVDBACFBNUSUTVFVDOZUSDPZQBGZVHEPZRBGEBSZVJIZJZBGZEBVHLMZSZTZDBSZ
      CVOGZVDOZDBSUTVGOUSBUAZVRUSWAVRKDEBFUBUCUDVQVTDBVPVIVTVKVNVDECVOVJCUEZVMV
      CBWBVLVBVJCUFUGUHUIUJUKVTVGDABVHAUEZVSVFVDWCVOVECVHABLULUMUNUIUOUPUQUR $.
      $( [9-Jun-2013] $)

    $( If ` B ( x ) ` is a family of elements of ` U ` and the index set ` A `
       is an element of ` U ` , then the indexed union ` U_ x e. A B ` is also
       an element of ` U ` , where ` U ` is a Grothendieck's universe.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiun $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> U_ x e. A B e. U ) $=
      ( vy cgru wcel wral ciun wa cmpt crn cuni wf wfn wss eqid fnmpt cv wceq
      wrex cab rnmpt wi wal r19.29 eleq1 biimparc r19.23aivw syl ex 19.21aiv
      abss sylibr syl5eqss df-f sylanbrc gruurn 3expia syl5com dfiun2g unieqi
      syl6eqr eleq1d sylibrd com12 3impia ) DFGZBDGZCDGZABHZABCIZDGZVKVHVIJZVMV
      KVNABCKZLZMZDGZVMVKBDVONZVNVRVKVOBOVPDPVSABCVODVOQZRVKVPESZCTZABUAZEUBZDA
      EBCVOVTUCZVKWCWADGZUDZEUEWDDPVKWGEVKWCWFVKWCJVJWBJZABUAWFVJWBABUFWHWFABWB
      WFVJWACDUGUHUIUJUKULWCEDUMUNUOBDVOUPUQVHVIVSVRBDVOURUSUTVKVLVQDVKVLWDMVQA
      EBCDVAVPWDWEVBVCVDVEVFVG $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains unions of its elements.  (Contributed
       by Mario Carneiro, 17-Jun-2013.) $)
    gruuni $p |- ( ( U e. Univ /\ A e. U ) -> U. A e. U ) $=
      ( vx cgru wcel wa cuni cv ciun uniiun wral wss grutr dfss3 sylib gruiun
      3expia mpd syl5eqel ) BDEZABEZFZAGCACHZIZBCAJUBUCBECAKZUDBEZUBABLUEABMCAB
      NOTUAUEUFCAUCBPQRS $.
      $( [17-Jun-2013] $)

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> ran F e. U ) $=
      ( cgru wcel wf w3a crn cuni cpw wss simp1 gruurn grupw syl11anc pwuni
      a1i gruss syl111anc ) BDEZABEZABCFZGZTCHZIZJZBEZUDUFKZUDBETUAUBLZUCTUEBEU
      GUIABCMUEBNOUHUCUDPQUFUDBRS $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains image sets drawn from its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruima $p |- ( ( U e. Univ /\ Fun F /\ ( F " A ) C_ U ) ->
        ( A e. U -> ( F " A ) e. U ) ) $=
      ( cgru wcel wfun cima wss w3a wa cdm cin cres crn wrel wceq simpl2
      funrel resres resdm reseq1 syl syl5eqr rneqd df-ima syl6reqr 3syl wf
      simpl1 simpr inss2 a1i gruss syl111anc wfn wfo funforn fof sylbi inss1
      fssres sylancl ffn simpl3 sseq1d mpbid df-f sylanbrc grurn eqeltrd ex )
      BDEZCFZCAGZBHZIZABEZVNBEVPVQJZVNCCKZALZMZNZBVRVMCOZVNWBPZVLVMVOVQQZCRZWCW
      BCAMZNVNWCWAWGWCWACVSMZAMZWGCVSASWCWHCPWIWGPCTWHCAUAUBUCUDCAUEUFZUGVRVLVT
      BEZVTBWAUHZWBBEVLVMVOVQUIZVRVLVQVTAHZWKWMVPVQUJWNVRVSAUKULAVTBUMUNVRWAVTU
      OZWBBHZWLVRVMWOWEVMVTCNZWAUHZWOVMVSWQCUHZVTVSHWRVMVSWQCUPWSCUQVSWQCURUSVS
      AUTVSWQVTCVAVBVTWQWAVCUBUBVRVOWPVLVMVOVQVDVRVNWBBVRVMWDWEVMWCWDWFWJUBUBVE
      VFVTBWAVGVHVTBWAVIUNVJVK $.
      $( [9-Jun-2013] $)

    $( Any element of an element of a Grothendieck's universe is also an
       element of the universe.  (Contributed by Mario Carneiro,
       9-Jun-2013.) $)
    gruel $p |- ( ( U e. Univ /\ A e. U /\ B e. A ) -> B e. U ) $=
      ( cgru wcel wa grutr sseld 3impia ) CDEZACEZBAEBCEJKFACBACGHI $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the singletons of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grusn $p |- ( ( U e. Univ /\ A e. U ) -> { A } e. U ) $=
      ( cgru wcel wa csn cpr dfsn2 grupr 3anidm23 syl5eqel ) BCDZABDZEAFAAGZBAH
      LMNBDAABIJK $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains ordered pairs of its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruop $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> <. A , B >. e. U ) $=
      ( cgru wcel w3a cop csn cpr df-op simp1 grusn 3adant3 grupr syl111anc
      syl5eqel ) CDEZACEZBCEZFZABGAHZABIZIZCABJTQUACEZUBCEUCCEQRSKQRUDSACLMABCN
      UAUBCNOP $.
      $( [10-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains binary unions of its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      gruun $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A u. B ) e. U ) $=
        ( vx cgru wcel w3a cun cpr cv ciun cuni uniiun wceq uniprg 3adant1
        syl5reqr wral simp1 grupr wa wo visset elpr eleq1a jaao syl5bi
        r19.21aiv gruiun syl111anc eqeltrd ) CEFZACFZBCFZGZABHZDABIZDJZKZCUOUSU
        QLZUPDUQMUMUNUTUPNULABCCOPQUOULUQCFURCFZDUQRZUSCFULUMUNSABCTUMUNVBULUMU
        NUAZVADUQURUQFURANZURBNZUBVCVAURABDUCUDUMVDVAUNVEACURUEBCURUEUFUGUHPDUQ
        URCUIUJUK $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains binary cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruxp $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A X. B ) e. U ) $=
      ( cgru wcel w3a cun cxp gruun cpw grupw wss xpsspw gruss mp3an3 syldan
      3ad2antl1 mpdan ) CDEZACEZBCEZFABGZCEZABHZCEZABCISTUCUEUASUCUBJZCEZUEUBCK
      SUGUFJZCEZUEUFCKSUIUDUHLUEABMUHUDCNOPPQR $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all powers of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grumap $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A ^m B ) e. U ) $=
      ( cgru wcel w3a cxp cpw cmap co wss simp1 gruxp 3com23 grupw syl11anc
      mapsspw 3ad2ant3 gruss syl111anc ) CDEZACEZBCEZFZUABAGZHZCEZABIJZUFKZUHCE
      UAUBUCLZUDUAUECEZUGUJUAUCUBUKBACMNUECOPUCUAUIUBABCQRUFUHCST $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruixp $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> X_ x e. A B e. U ) $=
      ( cgru wcel wral w3a ciun cmap co cixp wss simp1 gruiun simp2 grumap
      syl111anc ixpssmapg 3adant1 gruss ) DEFZBDFZCDFABGZHZUBABCIZBJKZDFZABCLZU
      GMZUIDFUBUCUDNZUEUBUFDFUCUHUKABCDOUBUCUDPUFBDQRUCUDUJUBABCDDSTUGUIDUAR $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed intersections of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiin $p |- ( ( U e. Univ /\ E. x e. A B e. U ) -> |^|_ x e. A B e. U ) $=
      ( vy cgru wcel wrex ciin ax-17 hbii1 cv hbel wss iinss2 gruss syl3an3
      3exp com23 r19.23ad imp ) DFGZCDGZABHABCIZDGZUBUCUEABUBAJAEEUDDAEBCKELDGA
      JMUBUCALBGZUEUBUCUFUEUFUBUCUDCNUEABCOCUDDPQRSTUA $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all functions on its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruf $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> F e. U ) $=
      ( vx vy vz cgru wcel wf w3a cv cfv cop wceq wa copab crn wfn simp3 ffn
      fnasrn 3syl simp1 simp2 wral cvv opex a1i rgen eqid fnopab2g mpbi fveq2
      opeq12 mpdan fvopab4 adantl simpl1 gruel 3expa 3adantl3 ffvelrn
      3ad2antl3 gruop syl111anc eqeltrd r19.21aiva ffnfv sylanbrc grurn ) BGHZA
      BHZABCIZJZCDKZAHZEKVOVOCLZMZNODEPZQZBVNVMCARCVTNVKVLVMSABCTDEACUAUBVNVKVL
      ABVSIZVTBHVKVLVMUCVKVLVMUDVNVSARZFKZVSLZBHZFAUEWAWBVNVRUFHZDAUEWBWFDAWFVP
      VOVQUGUHUIDEAVRVSVSUJZUKULUHVNWEFAVNWCAHZOZWDWCWCCLZMZBWHWDWKNVNDEWCVRWKA
      VSVOWCNVQWJNVRWKNVOWCCUMVOVQWCWJUNUOWGWCWJUGUPUQWIVKWCBHZWJBHZWKBHVKVLVMW
      HURVKVLWHWLVMVKVLWHWLAWCBUSUTVAVMVKWHWMVLABWCCVBVCWCWJBVDVEVFVGFABVSVHVIA
      BVSVJVEVF $.
      $( [10-Jun-2013] $)

    $( A Grothendieck's universe contains all subsets of itself that are
       equipotent to an element of the universe.  (Contributed by Mario
       Carneiro, 9-Jun-2013.) $)
    gruen $p |- ( ( U e. Univ /\ A C_ U /\ ( B e. U /\ B ~~ A ) )
        -> A e. U ) $=
      ( vy cgru wcel wss cen wbr wa wi cv wf1o wex cvv wb ssexg breng syl wfo
      f1ofo w3a crn wceq simp3l forn wf fof fss sylan grurn syl3an3 eqeltrrd
      3expia exp3a syl5 19.23adv com3r expdimp com23 sylbid imp3a ancoms
      3impia ) CEFZACGZBCFZBAHIZJZACFZVFVEVIVJKVFVEJZVGVHVJVKVHVGVJVKVHBADLZMZD
      NZVGVJKVKAOFVHVNPACEQBAODRSVKVGVNVJVFVEVGVNVJKVEVGJZVNVFVJVOVMVFVJKZDVMBA
      VLTZVOVPBAVLUAVOVQVFVJVEVGVQVFJZVJVEVGVRUBZVLUCZACVSVQVTAUDVEVGVQVFUEBAVL
      UFSVRVEVGBCVLUGZVTCFVQBAVLUGVFWABAVLUHBACVLUIUJBCVLUKULUMUNUOUPUQURUSUTVA
      UTVBVCVD $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d u x y A $.
    $( The intersection of a family of universes is a universe.  (Contributed
       by Mario Carneiro, 9-Jun-2013.) $)
    intgru $p |- ( ( A C_ Univ /\ A =/= (/) ) -> |^| A e. Univ ) $=
      ( vx vy vu cgru wss c0 wne wa cint cvv wcel wtr cv cpw cpr wral crn cuni
      cmap co w3a simpr intex sylib dfss3 elgrug ibi simpld ralimi sylbi trint
      syl adantr visset elint2 grupw ex ral2imi syl5bi pwex syl6ibr imp
      adantlr wi r19.26 grupr 3expia sylbir prex sylan2b r19.21aiv wf wb
      elmapg mpan2 ad2antlr intss1 fss sylan2 r19.21aiva gruurn syl5 rnex
      uniex sylbid 3jca sylanb biimpar syl12anc ) AEFZAGHZIZAJZKLZWNMZBNZOZWNLZ
      WQCNZPZWNLZCWNQZWTRZSZWNLZCWNWQTUAZQZUBZBWNQZWNELZWMWLWOWKWLUCAUDZUEWKWPW
      LWKDNZMZDAQZWPWKXMELZDAQZXODAEUFZXPXNDAXPXNWRXMLZXAXMLZCXMQXEXMLZCXMWQTUA
      QUBBXMQZXPXNYBIBCXMEUGUHUIUJUKDAULUMUNWKXQWLXJXRXQWLIZXIBWNYCWQWNLZIZWSXC
      XHXQYDWSWLXQYDWSXQYDXSDAQZWSYDWQXMLZDAQZXQYFDWQABUOZUPZXPYGXSDAXPYGXSWQXM
      UQURUSUTDWRAWQYIVAUPVBVCVDXQYDXCWLXQYDIZXBCWNYDXQYHWTWNLZXBVEYJYLWTXMLZDA
      QZXQYHIZXBDWTACUOZUPYOYNXTDAQZXBYOXPYGIZDAQZYNYQVEXPYGDAVFZYRYMXTDAXPYGYM
      XTWQWTXMVGVHUSVIDXAAWQWTVJUPVBUTVKVLVDYEXFCXGYEWTXGLZWQWNWTVMZXFWLUUAUUBV
      NZXQYDWLWOUUCXLWOWQKLUUCYIWNWQWTKKVOVPUKVQXQYDUUBXFVEWLYKUUBYADAQZXFUUBWQ
      XMWTVMZDAQZYKUUDUUBUUEDAXMALUUBWNXMFUUEXMAVRWQWNXMWTVSVTWAYDXQYHUUFUUDVEZ
      YJYOYSUUGYTYRUUEYADAXPYGUUEYAWQXMWTWBVHUSVIVKWCDXEAXDWTYPWDWEUPVBVDWFVLWG
      WAWHWOXKWPXJIBCWNKUGWIWJ $.
      $( [9-Jun-2013] $)

    $d u U $.
    $( The intersection of a universe with a class that acts like a universe is
       another universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    ingru $p |- ( ( Tr A /\ A. x e. A ( ~P x e. A /\
          A. y e. A { x , y } e. A /\ A. y ( y : x --> A -> U. ran y e. A ) ) )
        -> ( U e. Univ -> ( U i^i A ) e. Univ ) ) $=
      ( vu cgru wcel wtr cv cpw cpr wral wf crn cuni wi wal w3a wa cin wceq
      ineq1 eleq1d imbi2d cmap co elgrug ibi trin ex wss inss1 ssralv ax-mp
      inss2 df-3an elin simplbi2 ral2imi syl2im im2anan9 visset mapss mp2b
      inex1 elmap fss mpan2 sylbi imim1i alimi df-ral sylibr 3impa syl5bi
      syl6ibr syl cvv wb vtoclga com12 ) DFGCHZAIZJZCGZWCBIZKZCGZBCLZWCCWFMZWFN
      OZCGZPZBQZRZACLZSZDCTZFGZWQEIZCTZFGZPWQWSPEDFWTDUAZXBWSWQXCXAWRFWTDCUBUCU
      DWTFGZWQXAHZWDXAGZWGXAGZBXALZWKXAGZBXAWCUEUFZLZRZAXALZSZXBXDWTHZWDWTGZWGW
      TGZBWTLZWKWTGZBWTWCUEUFZLZRZAWTLZSZWQXNPXDYDABWTFUGUHXOWBXEYCWPXMXOWBXEWT
      CUIUJYCYBAXALZWPWOAXALZXMXAWTUKZYCYEPWTCULZYBAXAWTUMUNXACUKZWPYFPWTCUOZWO
      AXACUMUNYBWOXLAXAYBWOXFXHSZXKSZXLWOWEWISZWNSZYBYLWEWIWNUPXPXRYAYNYLPXPXRS
      YMYKYAWNXKXPWEXFXRWIXHXFXPWEWDWTCUQURXRXQBXALZWIWHBXALZXHYGXRYOPYHXQBXAWT
      UMUNYIWIYPPYJWHBXACUMUNXQWHXGBXAXGXQWHWGWTCUQURUSUTVAYAXSBXJLZWNWLBXJLZXK
      YGXJXTUKYAYQPYHXAWTWCEVBZAVBZVCXSBXJXTUMVDWNWFXJGZWLPZBQYRWMUUBBUUAWJWLUU
      AWCXAWFMZWJXAWCWFWTCYSVEZYTVFUUCYIWJYJWCXACWFVGVHVIVJVKWLBXJVLVMXSWLXIBXJ
      XIXSWLWKWTCUQURUSUTVAVNVOXFXHXKUPVPUSUTVAVQXAVRGXBXNVSUUDABXAVRUGUNVPVTWA
      $.
      $( [10-Jun-2013] $)

    $( The wellfounded part of a universe is another universe.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    wfgru $p |- ( U e. Univ -> ( U i^i U. ( R1 " On ) ) e. Univ ) $=
      ( vx vy cr1 con0 cima cuni wtr cv cpw wcel cpr wral wf crn wi wal w3a
      cgru cin wss r1elssi rgen dftr3 mpbir pwwf wa visset prss prex r1elss
      bitr4i biimpi r19.21aiva frn rnex uniwf sylbir syl ax-gen a1i 3jca ingru
      mp2an ) DEFGZHZBIZJVEKZVGCIZLZVEKZCVEMZVGVEVINZVIOZGVEKZPZCQZRZBVEMASKAVE
      TSKPVFVGVEUAZBVEMVSBVEVGUBUCBVEUDUEVRBVEVGVEKZVHVLVQVGUFVTVKCVEVTVIVEKUGZ
      VKWAVJVEUAVKVGVIVEBUHCUHZUIVJVGVIUJUKULUMUNVQVTVPCVMVNVEUAZVOVGVEVIUOWCVN
      VEKVOVNVIWBUPUKVNUQURUSUTVAVBUCBCVEAVCVD $.
      $( [17-Jun-2013] $)

    $d B x y $.  $d U x y $.
    $( Each ordinal that is comparable with an element of the universe is in
       the universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    grudomon $p |- ( ( U e. Univ /\ A e. On /\ ( B e. U /\ A ~<_ B ) ) ->
        A e. U ) $=
      ( vx vy cgru wcel cdom wbr wa con0 wi cv wceq breq1 eleq1 imbi12d imbi2d
      wral r19.21v w3a simpl1 cvv wss visset onelss imp ssdomg mpsyl sylan
      simplr domtr syl11anc pm2.27 syl ralimdva dfss3 cen wex wb simp3 domeng
      biimpa gruss 3expia 3adant1 adantr ensym a1i anim12d ancomsd eximdv
      simpl2 gruen 3com23 3exp 19.23adv syld mpd syl5bir ex com23 3expib a2d
      syl5bi tfis3 com3l impr 3impia ) CFGZBCGZABHIZJZAKGZACGZWJWMWNWOWJWKWLWNW
      OLWNWJWKJZWLWOWPDMZBHIZWQCGZLZLZWPEMZBHIZXBCGZLZLZWPWLWOLZLDEAWQXBNZWTXEW
      PXHWRXCWSXDWQXBBHOWQXBCPQRWQANZWTXGWPXIWRWLWSWOWQABHOWQACPQRXFEWQSWPXEEWQ
      SZLWQKGZXAWPXEEWQTXKWPXJWTXKWJWKXJWTLXKWJWKUAZWRXJWSXLWRXJWSLXLWRJZXJXDEW
      QSZWSXMXEXDEWQXMXBWQGZJZXCXEXDLXPXBWQHIZWRXCXMXKXOXQXKWJWKWRUBXBUCGXKXOJX
      BWQUDZXQEUEZXKXOXRWQXBUFUGXBWQUCUHUIUJXLWRXOUKXBWQBULUMXCXDUNUOUPXNWQCUDZ
      XMWSEWQCUQXMWQXBURIZXBBUDZJZEUSZXTWSLZXLWRYDXLWKWRYDUTXKWJWKVAEWQBCVBUOVC
      XMYDXDXBWQURIZJZEUSZYEXMYCYGEXMYBYAYGXMYBXDYAYFXLYBXDLZWRWJWKYIXKWJWKYBXD
      BXBCVDVEVFVGYAYFLXMWQXBXSVHVIVJVKVLXMWJYHYELXKWJWKWRVMWJYGYEEWJYGXTWSWJXT
      YGWSWQXBCVNVOVPVQUOVRVSVTVRWAWBWCWDWEWFWGWHWIVO $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d U x y $.
    gruina.1 $e |- A = ( U i^i On ) $.
    $( If a Grothendieck's universe ` U ` is nonempty, then the height of the
       ordinals in ` U ` is a strongly inaccessible cardinal.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    gruina $p |- ( ( U e. Univ /\ U =/= (/) ) -> A e. Inacc ) $=
      ( vx vy cgru wcel c0 wne wa ccf cfv wceq cv cpw csdm wbr wral cina wex
      wi n0 con0 cin wss 0ss gruss mp3an3 0elon jctir elin sylibr syl6eleqr
      ne0i syl expcom 19.23aiv sylbi impcom wn word cvv wtr cep wwe grutr
      r19.21aiva dftr3 tron trin sylancl inss2 epweon wess mp2 a1i df-ord
      sylanbrc inex1g elon2 syl5eqel adantr eloni ordirr biimpri mtod ccrd
      cuni cab cint wlim com wrex inss1 eqsstri sseli cdom cen visset pwex
      cardid ensymi grupw syldan cardon endom ax-mp grudomon mp3an2 mpanr2
      onelss sylc ssdomg mpsyl endomtr sylancr canth2 sdomdomtr mpani sylan2
      inawinalem winainflem syl111anc wb sdomtr iscard cardlim sseq2 limeq
      bibi12d mpbii mpbid cflm syl11anc eleq1 mpbiri abssi fvex syl6eqelr
      intex onint eqeltrd eqeq1 anbi1d exbidv elab sylib w3a simp2rr simp1l
      simp2rl syl6ss 3ad2ant3 simp2l breq1 gruen syl112anc gruuni 3exp
      19.23adv mpd wo cfon cfle onsseleq mpan ord elina ) BFGZBHIZJZAHIZAKLZAMZ
      DNZOZAPQZDARZASGZUVJUVIUVLUVJUVOBGZDTUVIUVLUAZDBUBUVTUWADUVIUVTUVLUVIUVTJ
      ZHAGUVLUWBHBUCUDZAUWBHBGZHUCGZJHUWCGUWBUWDUWEUVIUVTHUVOUEUWDUVOUFUVOHBUGU
      HUIUJHBUCUKULCUMAHUNUOUPUQURUSZUVKUVMAGZUTZUVNUVKUWGABGZUVKAUCGZUWIUTUVIU
      WJUVJUVIAUWCUCCUVIUWCVAZUWCVBGUWCUCGUVIUWCVCZUWCVDVEZUWKUVIBVCZUCVCUWLUVI
      UVOBUEZDBRUWNUVIUWODBUVOBVFVGDBVHULVIBUCVJVKUWMUVIUWCUCUEUCVDVEUWMBUCVLVM
      UWCUCVDVNVOVPUWCVQVRBUCFVSUWCVTVRWAZWBZUWJUWIAAGZUWJAVAUWRUTAWCAWDUOUWIUW
      JUWRUWIUWJJZAUWCAAUWCGUWSABUCUKWECUMUPWFUOUVKUVMENZWGLZMZUWTAUEZAUWTWHZMZ
      JZJZETZUWGUWIUAZUVKUVMUVOUXAMZUXFJZETZDWIZGUXHUVKUVMUXMWJZUXMUVKUWJAWKZUV
      MUXNMUWQUVKWLAUEZUXOUVKUVLUWJUVOUWTPQEAWMDARZUXPUWFUWQUVIUXQUVJUVIUWJUVRU
      XQUWPUVIUVQDAUVOAGZUVIUVTUVQABUVOAUWCBCBUCWNWOZWPUWBUWJUVPOZAWQQZUVQUVIUW
      JUVTUWPWBUWBUXTUXTWGLZWRQUYBAWQQZUYAUYBUXTUVPUVODWSZWTZWTUXTXAZXBUVIUVTUX
      TBGZUYCUVIUVTUVPBGUYGUVOBXCUVPBXCXDUYBUCGZUVIUYGJZUYBAUEZUYCUXTXEZUYIUWJU
      YBAGZUYJUVIUWJUYGUWPWBUYIUYBBGZUYHUYLUVIUYGUYBUXTWQQZUYMUYBUXTWRQUYNUYFUY
      BUXTXFXGUVIUYHUYGUYNJUYMUYKUYBUXTBXHXIXJUYKUYMUYHJZUYBUWCAUYBUWCGUYOUYBBU
      CUKWECUMVKAUYBXKXLUYBAUCXMXNXDUXTUYBAXOXPUWJUVPUXTPQUYAUVQUVPUYEXQUVPUXTA
      UCXRXSXLXTZVGZDEAYAXLWBDEAYBYCUVIUXPUXOYDZUVJUVIAWGLZAMZUYRUVIUWJUVOAPQZD
      ARUYTUWPUVIVUADAUVIUXRJUVOUVPPQUVQVUAUVOUYDXQUYPUVOUVPAYEXPVGDAYFVRUYTWLU
      YSUEZUYSWKZYDUYRAYGUYTVUBUXPVUCUXOUYSAWLYHUYSAYIYJYKUOWBYLDEAUCYMYNZUVKUX
      MUCUEUXMHIZUXNUXMGUXLDUCUXKUVOUCGZEUXJVUFUXFUXJVUFUXAUCGUWTXEUVOUXAUCYOYP
      WBUQYQUVKUXNVBGVUEUVKUXNUVMVBVUDAKYRZYSUXMYTULUXMUUAXPUUBUXLUXHDUVMVUGUVO
      UVMMZUXKUXGEVUHUXJUXBUXFUVOUVMUXAUUCUUDUUEUUFUUGUVKUXGUXIEUVKUXGUWGUWIUVK
      UXGUWGUUHZAUXDBUXCUXEUXBUVKUWGUUIVUIUVIUWTBGZUXDBGUVIUVJUXGUWGUUJZVUIUVIU
      WTBUEUVMBGZUVMUWTWRQZVUJVUKVUIUWTABUXCUXEUXBUVKUWGUUKUXSUULUWGUVKVULUXGAB
      UVMUXSWPUUMVUIUXBVUMUVKUXBUXFUWGUUNUXBVUMUXAUWTWRQUWTXAUVMUXAUWTWRUUOYPUO
      UWTUVMBUUPUUQUWTBUURYNUUBUUSUUTUVAWFUVKUWJUWHUVNUAUWQUWJUWGUVNUVMUCGZUWJU
      WGUVNUVBZAUVCVUNUWJJUVMAUEVUOAUVDUVMAUVEYKUVFUVGUOUVAUVIUVRUVJUYQWBUVSUVL
      UVNUVRUUHDAUVHWEYC $.
      $( [17-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 1.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    grur1a $p |- ( U e. Univ -> ( R1 ` A ) C_ U ) $=
      ( vx vy cgru wcel cr1 cfv wss c0 wceq wi con0 cin inss1 eqsstri sseq2
      mpbii ss0 0ss fveq2 r10 syl6eq sseq1d mpbiri 3syl a1i wne wa cv ciun
      cina cwina gruina inawina wlim winaon winalim r1lim syl11anc wral inss2
      sseli csuc eleq1 eleq1d imbi12d simpr elelsuc word wb ne0i syl sylan2
      eloni ordsucelsuc syl5ibr mpd imim1i com12 cpw grupw ex adantr r1suc
      biimprcd syl6 syld com23 com4r ontr1 pm2.27 exp3a com3r sylc imp
      ralimdva gruiun 3expia cvv visset mpan biimprd sylan9r exp32 com34
      tfinds2 impcom grutr syldan r19.21aiva iunss sylibr eqsstrd pm2.61dne )
      BFGZAHIZBJZBKBKLZXSMXQXTAKJZAKLZXSXTABJYAABNOZBCBNPQZBKARSATYBXSKBJBUAYBX
      RKBYBXRKHIZKAKHUBUCUDUEUFUGUHXQBKUIZXSXQYFUJZXRDADUKZHIZULZBYGAUMGZAUNGZX
      RYJLZABCUOZAUPZYLANGZAUQYMAURZAUSDANUTVAUGXQYJBJZYFXQYIBJZDAVBYRXQYSDAXQY
      HAGZYIBGZYSYTXQUUAYTYHNGZXQUUAMANYHAYCNCBNVCQVDUUBXQYTUUAYTUUAMKAGZKBGZMZ
      EUKZAGZUUFHIZBGZMZUUFVEZAGZUUKHIZBGZMXQDEYHKLZYTUUCUUAUUDYHKAVFUUOYIKBUUO
      YIYEKYHKHUBUCUDVGVHYHUUFLZYTUUGUUAUUIYHUUFAVFUUPYIUUHBYHUUFHUBVGVHYHUUKLZ
      YTUULUUAUUNYHUUKAVFUUQYIUUMBYHUUKHUBVGVHUUEXQABKYDVDUHXQUUJUULUUFNGZUUNXQ
      UULUUJUURUUNMZXQUULUUJUUSMXQUULUJZUUJUUIUUSUUJUUTUUIUUTUUGUUIUUTUULUUGXQU
      ULVIUULUUGUUTUUKAVEGZUUKAVJUUTYPAVKUUGUVAVLUULXQYFYPUULUUKBGYFABUUKYDVDBU
      UKVMVNYGYKYLYPYNYOYQUGZVOAVPUUFAVQUGVRVSVTWAUUTUUIUUHWBZBGZUUSXQUUIUVDMUU
      LXQUUIUVDUUHBWCWDWEUURUUNUVDUURUUMUVCBUUFWFVGWGWHWIWDWJWKYHUQZXQYTUUJEYHV
      BZUUAUVEXQYTUVFUUAMXQYTUJZUVFEYHUUHULZBGZUVEUUAUVGUVFUUIEYHVBZUVIUVGUUJUU
      IEYHUVGUUFYHGZUUJUUIMZUVGYTYPUVKUVLMXQYTVIYTXQYFYPYTYHBGZYFABYHYDVDZBYHVM
      VNUVBVOYPUVKYTUVLYPUVKYTUVLYPUVKYTUJUUGUVLUUFYHAWLUUGUUIWMWHWNWOWPWQWRYTX
      QUVMUVJUVIMUVNXQUVMUVJUVIEYHUUHBWSWTVOWIUVEUUAUVIUVEYIUVHBYHXAGUVEYIUVHLD
      XBEYHXAUTXCVGXDXEXFXGXHWOVSXIYIBXJXKXLDAYIBXMXNWEXOWDXP $.
      $( [23-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 2.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    grur1 $p |- ( ( U e. Univ /\ U e. U. ( R1 " On ) ) -> U = ( R1 ` A ) ) $=
      ( vy vx cgru wcel cr1 con0 cima cuni wa cfv wss wn cv crnk wceq wrex wex
      nss wo simprr wb simplr simprl r1elssi sseld sylc cina cwina c0 wne ne0i
      gruina sylan2 inawina winaon 3syl ad2ant2r rankr1ag syl11anc mtbid wi
      w3o rankon word eloni ordtri3or syl2an sylancr 3orass sylib ord mpd
      fveq2 eqeq1d rcla4ev ex ad2antrl ctc tcrank syl eleq2d wtr grutr wral
      r19.21aiva dftr3 sylibr adantr cvv visset tcmin ax-mp wfun wf rankf ffun
      fvelima mpan ssrexv syl2im sylbid jaod 19.23adv syl5bi cdom wbr simpll
      ccf ad2antll cpw csdm w3a elina simp2 sylbi eqtrd rankcf fvex domtri
      mp2an mpbir syl6eqbrr grudomon syl112anc cin elin biimpri syl6eleqr
      ordirr adantl pm2.65i pm2.21i exp32 r19.23adv syld pm2.18d grur1a eqssd
      ) BFGZBHIJKZGZLZBAHMZUUEBUUFNZUUEUUGOZDPZQMZARZDBSZUUGUUHEPZBGZUUMUUFGZOZ
      LZETUUEUULEBUUFUAUUEUUQUULEUUEUUQUULUUEUUQLZUUMQMZARZAUUSGZUBZUULUURUUSAG
      ZOZUVBUURUUOUVCUUEUUNUUPUCUURUUMUUCGZAIGZUUOUVCUDUURUUDUUNUVEUUBUUDUUQUEU
      UEUUNUUPUFUUDBUUCUUMBUGUHUIZUUBUUNUVFUUDUUPUUBUUNLZAUJGZAUKGZUVFUUNUUBBUL
      UMZUVIBUUMUNABCUOZUPAUQZAURZUSZUTUUMAVAVBVCUUBUUNUVDUVBVDUUDUUPUVHUVCUVBU
      VHUVCUUTUVAVEZUVCUVBUBUVHUUSIGZUVFUVPUUMVFUVOUVQUUSVGAVGZUVPUVFUUSVHAVHZU
      USAVIVJVKUVCUUTUVAVLVMVNUTVOUURUUTUULUVAUUNUUTUULVDUUEUUPUUNUUTUULUUKUUTD
      UUMBUUIUUMRUUJUUSAUUIUUMQVPVQVRVSVTUURUVAAQUUMWAMZJZGZUULUURUUSUWAAUURUVE
      UUSUWARUVGUUMWBWCWDUUBUUNUWBUULVDUUDUUPUVHUVTBNZUWBUUKDUVTSZUULUVHUUMBNZB
      WEZUWCUUMBWFZUUBUWFUUNUUBUWEEBWGUWFUUBUWEEBUWGWHEBWIWJWKUUMWLGUWEUWFLUWCV
      DEWMUUMBWLWNWOVBQWPZUWBUWDUUCIQWQUWHWRUUCIQWSWODAUVTQWTXAUUKDUVTBXBXCUTXD
      XEVOVSXFXGUUEUUKUUGDBUUEUUIBGZUUKUUGUUEUWIUUKLZLZABGZUVFUUGUWKUUBUVFUWIAU
      UIXHXIUWLUUBUUDUWJXJUWKUVIUVJUVFUUBUWIUVIUUDUUKUWIUUBUVKUVIBUUIUNUVLUPUTZ
      UVMUVNUSZUUEUWIUUKUFUWKAUUJXKMZUUIXHUWKUWOAXKMZAUUKUWOUWPRUUEUWIUUJAXKVPX
      LUWKUVIUWPARZUWMUVIAULUMZUWQUUMXMAXNXIEAWGZXOUWQEAXPUWRUWQUWSXQXRWCXSUWOU
      UIXHXIZUUIUWOXNXIOZUUIXTUWOWLGUUIWLGUWTUXAUDUUJXKYADWMUWOUUIWLWLYBYCYDYEA
      UUIBYFYGUWNUWLUVFLZUUGUXBAAGZUXBABIYHZAAUXDGUXBABIYIYJCYKUVFUXCOZUWLUVFUV
      RUXEUVSAYLWCYMYNYOVBYPYQYRYSUUBUUFBNUUDABCYTWKUUA $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d T x y $.
    $( Grothendieck's universes are the same as transitive Tarski's classes,
       part one: a transitive Tarski class is a universe.  (The hard work is in
       ~ tskuni .)  (Contributed by Mario Carneiro, 17-Jun-2013.) $)
    grutsk1 $p |- ( ( T e. Tarski /\ Tr T ) -> T e. Univ ) $=
      ( vx vy ctsk wcel wtr wa cgru cv cpw cpr wral crn cuni cmap co w3a simpr
      tskpw adantlr tskpr 3expa r19.21aiva wf wb elmapg tskurn 3expia sylbid
      r19.21aiv 3jca jca elgrug adantr mpbird ) ADEZAFZGZAHEZUQBIZJAEZUTCIZKAEZ
      CALZVBMNAEZCAUTOPZLZQZBALZGZURUQVIUPUQRURVHBAURUTAEZGZVAVDVGUPVKVAUQUTAST
      UPVKVDUQUPVKGVCCAUPVKVBAEVCUTVBAUAUBUCTVLVECVFVLVBVFEZUTAVBUDZVEUPVKVMVNU
      EUQAUTVBDAUFTURVKVNVEUTAVBUGUHUIUJUKUCULUPUSVJUEUQBCADUMUNUO $.
      $( [17-Jun-2013] $)
  $}

  ${
    $d x y z $.
    $( Grothendieck's universes are the same as transitive Tarski's classes.
       (The proof in the forward direction requires Foundation.)  (Contributed
       by Mario Carneiro, 24-Jun-2013.) $)
    grutsk $p |- Univ = { x e. Tarski | Tr x } $=
      ( vy vz cgru cv wtr ctsk crab wcel wa c0 wceq wi 0tsk eleq1 mpbiri a1i
      wne con0 cin cr1 cfv cima cuni cvv visset unir1 eleqtrri eqid grur1
      mpan2 adantr cina gruina inatsk syl eqeltrd ex pm2.61dne cpw cpr wral
      crn cmap co w3a elgrug ibi simpld jca grutsk1 impbii treq elrab bitr4i
      eqriv ) BDAEZFZAGHZBEZDIZVTGIZVTFZJZVTVSIWAWDWAWBWCWAWBVTKVTKLZWBMWAWEWBK
      GINVTKGOPQWAVTKRZWBWAWFJZVTVTSTZUAUBZGWAVTWILZWFWAVTUASUCUDZIWJVTUEWKBUFU
      GUHWHVTWHUIZUJUKULWGWHUMIWIGIWHVTWLUNWHUOUPUQURUSWAWCVQUTVTIVQCEZVAVTICVT
      VBWMVCUDVTICVTVQVDVEVBVFAVTVBZWAWCWNJACVTDVGVHVIVJVTVKVLVRWCAVTGVQVTVMVNV
      OVP $.
      $( [24-Jun-2013] $)
  $}

$( I decided to comment out the Card class for now.  We have too many
   definitions, and this one doesn't really buy us much. $)

$(
  @c Card @. @( Class of all cardinal numbers @)

  @( Extend class definition to include the class of all cardinal numbers. @)
  ccdn @a class Card @.

  @( Define the class of all cardinal numbers.  The notation "Card" is used
     in Exercise 5(G) of [JustWeese] p. 174.  It should not be confused with
     the lower-case "card" for the cardinal number function ~ df-card .  @)
  df-cardn @a |- Card = ( om u. ran aleph ) @.

  @( Membership in the class of cardinal numbers. @)
  elcard @p |- ( A e. Card <-> ( card ` A ) = A ) @=
    ( ccdn wcel com cale crn cun ccrd cfv wceq df-cardn eleq2i iscard3 bitr4i )
    ABCADEFGZCAHIAJBOAKLAMN @.
    @( [31-Dec-2004] @) @( [23-Sep-2004] @)

  @( The cardinality of a set is a cardinal number. @)
  cardel @p |- ( card ` A ) e. Card @=
    ( ccrd cfv ccdn wcel wceq cardidm elcard mpbir ) ABCZDEJBCJFAGJHI @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( A natural number is a cardinal number. @)
  nncard @p |- ( A e. om -> A e. Card ) @=
    ( com wcel ccrd cfv wceq ccdn cardnn elcard sylibr ) ABCADEAFAGCAHAIJ @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( An alternate definition of the class of all cardinal numbers. @)
  cardnum2 @p |- Card = { x | ( card ` x ) = x } @=
    ( ccdn com cale crn cun cv ccrd cfv wceq cab df-cardn wcel iscard3 bicomi
    abbi2i eqcomi eqtr4i ) BCDEFZAGZHITJZAKZLSUBUAASUATSMTNOPQR @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of cardinal numbers is a proper class.  Exercise 5(G)(b) of
     [JustWeese] p. 174. @)
  cardnprc @p |- -. Card e. _V @=
    ( vx ccdn cvv wcel cv ccrd cfv wceq cab cardprc cardnum2 eleq1i mtbir ) BCD
    AEZFGNHAIZCDAJBOCAKLM @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of transfinite cardinals (the range of the aleph function) is a
     proper class. @)
  alephprc2 @p |- -. ran aleph e. _V @=
    ( cale crn cvv wcel com cun ccdn cardnprc df-cardn eleq1i mtbi omex wa
    unexb biimpi mpan mto ) ABZCDZERFZCDZGCDUAHGTCIJKECDZSUALUBSMUAERNOPQ @.
    @( [19-Feb-2005] @) @( [23-Sep-2004] @)
$)


