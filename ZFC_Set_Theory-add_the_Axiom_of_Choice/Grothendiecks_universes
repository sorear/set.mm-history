$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Grothendieck's universes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Univ $. $( The class of all Grothendieck's universes. $)

  $( Extend class notation to include the class of all Grothendieck's
     universes. $)
  cgru $a class Univ $.

  ${
    $d u x y $.
    $( A Grothendieck's universe is a set that is closed with respect to all
       the operations that are common in set theory: pairs, powersets, unions,
       intersections, cross products etc.  Grothendieck and alii,
       S&eacute;minaire de G&eacute;om&eacute;trie Alg&eacute;brique 4,
       Expos&eacute; I, p. 185.  It was designed to give a precise meaning to
       the concepts of categories of sets, groups... $)
    df-gruNEW $a |- Univ = { u | ( Tr u /\ A. x e. u ( ~P x e. u /\
      A. y e. u { x , y } e. u /\ A. y e. ( u ^m x ) U. ran y e. u ) ) } $.
  $}

  ${
    $d U u x y $.
    $( Properties of a Grothendieck's universe. $)
    elgrug $p |- ( U e. V ->
      ( U e. Univ <-> ( Tr U /\ A. x e. U ( ~P x e. U /\
        A. y e. U { x , y } e. U /\ A. y e. ( U ^m x ) U. ran y e. U ) ) ) ) $=
      ( vu cgru wcel cv wtr cpw cpr wral crn cuni cmap co w3a eleq2 raleqbi1dv
      wa cab df-gruNEW eleq2i wceq treq raleqdv ralbidv bitrd 3anbi123d anbi12d
      opreq1 elabg syl5bb ) CFGCEHZIZAHZJZUNGZUPBHZKZUNGZBUNLZUSMNZUNGZBUNUPOPZ
      LZQZAUNLZTZEUAZGCDGCIZUQCGZUTCGZBCLZVCCGZBCUPOPZLZQZACLZTZFVJCABEUBUCVIVT
      ECDUNCUDZUOVKVHVSUNCUEVGVRAUNCWAURVLVBVNVFVQUNCUQRVAVMBUNCUNCUTRSWAVFVDBV
      PLVQWAVDBVEVPUNCUPOUKUFWAVDVOBVPUNCVCRUGUHUISUJULUM $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d U x y z $.  $d A u x y z $.  $d B y $.  $d F x y z $.
    $( A Grothendieck's universe is transitive, so each element is a subset of
       the universe.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grutr $p |- ( ( U e. Univ /\ A e. U ) -> A C_ U ) $=
      ( vx vy cgru wcel wtr wss cv cpw cpr wral crn cuni cmap co w3a elgrug ibi
      wa simpld trss imp sylan ) BEFZBGZABFZABHZUEUFCIZJBFUIDIZKBFDBLUJMNBFDBUI
      OPLQCBLZUEUFUKTCDBERSUAUFUGUHBAUBUCUD $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the powerset of each of its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grupw $p |- ( ( U e. Univ /\ A e. U ) -> ~P A e. U ) $=
      ( vy vx cgru wcel cpw cv cpr wral crn cuni cmap co w3a wi wtr elgrug ibi
      wa simprd simp1 ralimi wceq pweq eleq1d rcla4cv 3syl imp ) BEFZABFZAGZBFZ
      UJCHZGZBFZUNDHZIBFDBJZUQKLBFDBUNMNJZOZCBJZUPCBJUKUMPUJBQZVAUJVBVATCDBERSU
      AUTUPCBUPURUSUBUCUPUMCABUNAUDUOULBUNAUEUFUGUHUI $.
      $( [9-Jun-2013] $)

    $( Any subset of an element of a Grothendieck's universe is also an
       element.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruss $p |- ( ( U e. Univ /\ A e. U /\ B C_ A ) -> B e. U ) $=
      ( cgru wcel wss wa cpw wb elpw2g adantl grupw grutr syldan sylbird 3impia
      sseld ) CDEZACEZBAFZBCEZRSGZTBAHZEZUASUDTIRBACJKUBUCCBRSUCCEUCCFACLUCCMNQ
      OP $.
      $( [9-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains pairs derived from its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      grupr $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> { A , B } e. U ) $=
        ( vx vy cgru wcel cpr cv wral cpw crn cuni cmap w3a wceq eleq1d rcla4cv
        wi co wtr wa elgrug ibi preq2 3ad2ant2 com12 ralimdv syl5com preq1 syl6
        simprd com23 3imp ) CFGZACGZBCGZABHZCGZUOUQUPUSUOUQDIZBHZCGZDCJZUPUSSUO
        UTKCGZUTEIZHZCGZECJZVELMCGECUTNTJZOZDCJZUQVCUOCUAZVKUOVLVKUBDECFUCUDULU
        QVJVBDCVJUQVBVHVDUQVBSVIVGVBEBCVEBPVFVACVEBUTUEQRUFUGUHUIVBUSDACUTAPVAU
        RCUTABUJQRUKUMUN $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> U. ran F e. U ) $=
      ( vx vy cgru wcel wf crn cuni wa cmap co elmapg wi cpw wral wceq rcla4cv
      cv cpr w3a elgrug simprd rneq unieqd eleq1d 3ad2ant3 ralimi opreq2 eleq2d
      wtr ibi imbi1d 3syl imp sylbird 3impia ) BFGZABGZABCHZCIZJZBGZUSUTKVACBAL
      MZGZVDBACFBNUSUTVFVDOZUSDTZPBGZVHETZUABGEBQZVJIZJZBGZEBVHLMZQZUBZDBQZCVOG
      ZVDOZDBQUTVGOUSBULZVRUSWAVRKDEBFUCUMUDVQVTDBVPVIVTVKVNVDECVOVJCRZVMVCBWBV
      LVBVJCUEUFUGSUHUIVTVGDABVHARZVSVFVDWCVOVECVHABLUJUKUNSUOUPUQUR $.
      $( [9-Jun-2013] $)

    $( If ` B ( x ) ` is a family of elements of ` U ` and the index set ` A `
       is an element of ` U ` , then the indexed union ` U_ x e. A B ` is also
       an element of ` U ` , where ` U ` is a Grothendieck's universe.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiun $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> U_ x e. A B e. U ) $=
      ( vy cgru wcel wral ciun wa cmpt crn cuni wf wfn wss eqid fnmpt cv wrex
      wceq cab rnmpt wi wal r19.29 eleq1 biimparc r19.23aivw ex 19.21aiv sylibr
      syl syl5eqss sylanbrc gruurn 3expia syl5com dfiun2g unieqi syl6eqr eleq1d
      abss df-f sylibrd com12 3impia ) DFGZBDGZCDGZABHZABCIZDGZVKVHVIJZVMVKVNAB
      CKZLZMZDGZVMVKBDVONZVNVRVKVOBOVPDPVSABCVODVOQZRVKVPESZCUAZABTZEUBZDAEBCVO
      VTUCZVKWCWADGZUDZEUEWDDPVKWGEVKWCWFVKWCJVJWBJZABTWFVJWBABUFWHWFABWBWFVJWA
      CDUGUHUIUMUJUKWCEDVCULUNBDVOVDUOVHVIVSVRBDVOUPUQURVKVLVQDVKVLWDMVQAEBCDUS
      VPWDWEUTVAVBVEVFVG $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains unions of its elements.  (Contributed
       by Mario Carneiro, 17-Jun-2013.) $)
    gruuni $p |- ( ( U e. Univ /\ A e. U ) -> U. A e. U ) $=
      ( vx cgru wcel wa cuni cv ciun uniiun wss grutr dfss3 sylib gruiun 3expia
      wral mpd syl5eqel ) BDEZABEZFZAGCACHZIZBCAJUBUCBECAQZUDBEZUBABKUEABLCABMN
      TUAUEUFCAUCBOPRS $.
      $( [17-Jun-2013] $)

    $( A Grothendieck's universe contains the range of any function which takes
       values in the universe (see ~ gruiun for a more intuitive version).
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grurn $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> ran F e. U ) $=
      ( cgru wcel wf w3a crn cuni cpw wss simp1 gruurn grupw syl11anc pwuni a1i
      gruss syl111anc ) BDEZABEZABCFZGZTCHZIZJZBEZUDUFKZUDBETUAUBLZUCTUEBEUGUIA
      BCMUEBNOUHUCUDPQUFUDBRS $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains image sets drawn from its members.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruima $p |- ( ( U e. Univ /\ Fun F /\ ( F " A ) C_ U ) ->
        ( A e. U -> ( F " A ) e. U ) ) $=
      ( cgru wcel wfun cima wss w3a wa cdm cin cres crn wrel wceq simpl2 syl wf
      syl111anc funrel resres resdm reseq1 syl5eqr rneqd df-ima syl6reqr simpl1
      3syl simpr inss2 a1i gruss wfn wfo funforn fof sylbi inss1 fssres sylancl
      ffn simpl3 sseq1d mpbid df-f sylanbrc grurn eqeltrd ex ) BDEZCFZCAGZBHZIZ
      ABEZVNBEVPVQJZVNCCKZALZMZNZBVRVMCOZVNWBPZVLVMVOVQQZCUAZWCWBCAMZNVNWCWAWGW
      CWACVSMZAMZWGCVSAUBWCWHCPWIWGPCUCWHCAUDRUEUFCAUGUHZUJVRVLVTBEZVTBWASZWBBE
      VLVMVOVQUIZVRVLVQVTAHZWKWMVPVQUKWNVRVSAULUMAVTBUNTVRWAVTUOZWBBHZWLVRVMWOW
      EVMVTCNZWASZWOVMVSWQCSZVTVSHWRVMVSWQCUPWSCUQVSWQCURUSVSAUTVSWQVTCVAVBVTWQ
      WAVCRRVRVOWPVLVMVOVQVDVRVNWBBVRVMWDWEVMWCWDWFWJRRVEVFVTBWAVGVHVTBWAVITVJV
      K $.
      $( [9-Jun-2013] $)

    $( Any element of an element of a Grothendieck's universe is also an
       element of the universe.  (Contributed by Mario Carneiro,
       9-Jun-2013.) $)
    gruel $p |- ( ( U e. Univ /\ A e. U /\ B e. A ) -> B e. U ) $=
      ( cgru wcel wa grutr sseld 3impia ) CDEZACEZBAEBCEJKFACBACGHI $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains the singletons of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grusn $p |- ( ( U e. Univ /\ A e. U ) -> { A } e. U ) $=
      ( cgru wcel wa csn cpr dfsn2 grupr 3anidm23 syl5eqel ) BCDZABDZEAFAAGZBAH
      LMNBDAABIJK $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains ordered pairs of its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruop $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> <. A , B >. e. U ) $=
      ( cgru w3a cop csn cpr df-op simp1 grusn 3adant3 grupr syl111anc syl5eqel
      wcel ) CDPZACPZBCPZEZABFAGZABHZHZCABITQUACPZUBCPUCCPQRSJQRUDSACKLABCMUAUB
      CMNO $.
      $( [10-Jun-2013] $)

    ${
      $d B x $.
      $( A Grothendieck's universe contains binary unions of its elements.
         (Contributed by Mario Carneiro, 9-Jun-2013.) $)
      gruun $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A u. B ) e. U ) $=
        ( vx cgru wcel w3a cun cv ciun cuni uniiun wceq uniprg 3adant1 syl5reqr
        cpr wral simp1 eleq1a grupr wa visset elpr jaao syl5bi r19.21aiv gruiun
        wo syl111anc eqeltrd ) CEFZACFZBCFZGZABHZDABQZDIZJZCUOUSUQKZUPDUQLUMUNU
        TUPMULABCCNOPUOULUQCFURCFZDUQRZUSCFULUMUNSABCUAUMUNVBULUMUNUBZVADUQURUQ
        FURAMZURBMZUIVCVAURABDUCUDUMVDVAUNVEACURTBCURTUEUFUGODUQURCUHUJUK $.
        $( [9-Jun-2013] $)
    $}

    $( A Grothendieck's universe contains binary cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruxp $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A X. B ) e. U ) $=
      ( cgru wcel w3a cun cxp gruun cpw grupw wss xpsspw gruss mp3an3 3ad2antl1
      syldan mpdan ) CDEZACEZBCEZFABGZCEZABHZCEZABCISTUCUEUASUCUBJZCEZUEUBCKSUG
      UFJZCEZUEUFCKSUIUDUHLUEABMUHUDCNOQQPR $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all powers of its elements.
       (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    grumap $p |- ( ( U e. Univ /\ A e. U /\ B e. U ) -> ( A ^m B ) e. U ) $=
      ( cgru wcel w3a cxp cpw cmap co simp1 gruxp 3com23 grupw syl11anc mapsspw
      wss 3ad2ant3 gruss syl111anc ) CDEZACEZBCEZFZUABAGZHZCEZABIJZUFQZUHCEUAUB
      UCKZUDUAUECEZUGUJUAUCUBUKBACLMUECNOUCUAUIUBABCPRUFUHCST $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed cartesian products of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruixp $p |- ( ( U e. Univ /\ A e. U /\ A. x e. A B e. U )
        -> X_ x e. A B e. U ) $=
      ( cgru wcel wral w3a ciun cmap co wss simp1 gruiun simp2 grumap syl111anc
      cixp ixpssmapg 3adant1 gruss ) DEFZBDFZCDFABGZHZUBABCIZBJKZDFZABCRZUGLZUI
      DFUBUCUDMZUEUBUFDFUCUHUKABCDNUBUCUDOUFBDPQUCUDUJUBABCDDSTUGUIDUAQ $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains indexed intersections of its
       elements.  (Contributed by Mario Carneiro, 9-Jun-2013.) $)
    gruiin $p |- ( ( U e. Univ /\ E. x e. A B e. U ) -> |^|_ x e. A B e. U ) $=
      ( vy cgru wcel wrex ciin ax-17 hbii1 cv hbel wss gruss syl3an3 3exp com23
      iinss2 r19.23ad imp ) DFGZCDGZABHABCIZDGZUBUCUEABUBAJAEEUDDAEBCKELDGAJMUB
      UCALBGZUEUBUCUFUEUFUBUCUDCNUEABCSCUDDOPQRTUA $.
      $( [9-Jun-2013] $)

    $( A Grothendieck's universe contains all functions on its elements.
       (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    gruf $p |- ( ( U e. Univ /\ A e. U /\ F : A --> U ) -> F e. U ) $=
      ( vx vy vz cgru wcel wf cv cfv cop wceq wa wfn wral a1i syl111anc eqeltrd
      opex w3a copab crn simp3 ffn 3syl simp1 simp2 cvv rgen eqid fnopab2g mpbi
      fnasrn fveq2 opeq12 mpdan fvopab4 adantl gruel 3adantl3 ffvelrn 3ad2antl3
      simpl1 3expa gruop r19.21aiva ffnfv sylanbrc grurn ) BGHZABHZABCIZUAZCDJZ
      AHZEJVOVOCKZLZMNDEUBZUCZBVNVMCAOCVTMVKVLVMUDABCUEDEACUNUFVNVKVLABVSIZVTBH
      VKVLVMUGVKVLVMUHVNVSAOZFJZVSKZBHZFAPWAWBVNVRUIHZDAPWBWFDAWFVPVOVQTQUJDEAV
      RVSVSUKZULUMQVNWEFAVNWCAHZNZWDWCWCCKZLZBWHWDWKMVNDEWCVRWKAVSVOWCMVQWJMVRW
      KMVOWCCUOVOVQWCWJUPUQWGWCWJTURUSWIVKWCBHZWJBHZWKBHVKVLVMWHVDVKVLWHWLVMVKV
      LWHWLAWCBUTVEVAVMVKWHWMVLABWCCVBVCWCWJBVFRSVGFABVSVHVIABVSVJRS $.
      $( [10-Jun-2013] $)

    $( A Grothendieck's universe contains all subsets of itself that are
       equipotent to an element of the universe.  (Contributed by Mario
       Carneiro, 9-Jun-2013.) $)
    gruen $p |- ( ( U e. Univ /\ A C_ U /\ ( B e. U /\ B ~~ A ) )
        -> A e. U ) $=
      ( vy cgru wcel wss cen wbr wa wi cv wf1o wex cvv wb ssexg syl wf com23
      breng wfo f1ofo w3a crn wceq simp3l forn fof sylan grurn syl3an3 eqeltrrd
      fss 3expia exp3a syl5 19.23adv com3r expdimp sylbid imp3a ancoms 3impia )
      CEFZACGZBCFZBAHIZJZACFZVFVEVIVJKVFVEJZVGVHVJVKVHVGVJVKVHBADLZMZDNZVGVJKVK
      AOFVHVNPACEQBAODUARVKVGVNVJVFVEVGVNVJKVEVGJZVNVFVJVOVMVFVJKZDVMBAVLUBZVOV
      PBAVLUCVOVQVFVJVEVGVQVFJZVJVEVGVRUDZVLUEZACVSVQVTAUFVEVGVQVFUGBAVLUHRVRVE
      VGBCVLSZVTCFVQBAVLSVFWABAVLUIBACVLUNUJBCVLUKULUMUOUPUQURUSUTTVATVBVCVD $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d u x y A $.
    $( The intersection of a family of universes is a universe.  (Contributed
       by Mario Carneiro, 9-Jun-2013.) $)
    intgru $p |- ( ( A C_ Univ /\ A =/= (/) ) -> |^| A e. Univ ) $=
      ( vx vy vu cgru wss wa cvv wcel wtr cv wral co w3a elint2 ral2imi syl6ibr
      cmap adantlr wi c0 wne cint cpw cpr crn cuni simpr intex sylib elgrug ibi
      dfss3 simpld ralimi sylbi trint adantr visset grupw ex syl5bi pwex r19.26
      syl imp grupr 3expia sylbir sylan2b r19.21aiv wf wb elmapg mpan2 ad2antlr
      prex intss1 fss sylan2 r19.21aiva gruurn syl5 uniex sylbid sylanb biimpar
      rnex 3jca syl12anc ) AEFZAUAUBZGZAUCZHIZWNJZBKZUDZWNIZWQCKZUEZWNIZCWNLZWT
      UFZUGZWNIZCWNWQRMZLZNZBWNLZWNEIZWMWLWOWKWLUHAUIZUJWKWPWLWKDKZJZDALZWPWKXM
      EIZDALZXODAEUMZXPXNDAXPXNWRXMIZXAXMIZCXMLXEXMIZCXMWQRMLNBXMLZXPXNYBGBCXME
      UKULUNUOUPDAUQVEURWKXQWLXJXRXQWLGZXIBWNYCWQWNIZGZWSXCXHXQYDWSWLXQYDWSXQYD
      XSDALZWSYDWQXMIZDALZXQYFDWQABUSZOZXPYGXSDAXPYGXSWQXMUTVAPVBDWRAWQYIVCOQVF
      SXQYDXCWLXQYDGZXBCWNYDXQYHWTWNIZXBTYJYLWTXMIZDALZXQYHGZXBDWTACUSZOYOYNXTD
      ALZXBYOXPYGGZDALZYNYQTXPYGDAVDZYRYMXTDAXPYGYMXTWQWTXMVGVHPVIDXAAWQWTVQOQV
      BVJVKSYEXFCXGYEWTXGIZWQWNWTVLZXFWLUUAUUBVMZXQYDWLWOUUCXLWOWQHIUUCYIWNWQWT
      HHVNVOUPVPXQYDUUBXFTWLYKUUBYADALZXFUUBWQXMWTVLZDALZYKUUDUUBUUEDAXMAIUUBWN
      XMFUUEXMAVRWQWNXMWTVSVTWAYDXQYHUUFUUDTZYJYOYSUUGYTYRUUEYADAXPYGUUEYAWQXMW
      TWBVHPVIVJWCDXEAXDWTYPWHWDOQSWEVKWIWAWFWOXKWPXJGBCWNHUKWGWJ $.
      $( [9-Jun-2013] $)

    $d u U $.
    $( The intersection of a universe with a class that acts like a universe is
       another universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    ingru $p |- ( ( Tr A /\ A. x e. A ( ~P x e. A /\
          A. y e. A { x , y } e. A /\ A. y ( y : x --> A -> U. ran y e. A ) ) )
        -> ( U e. Univ -> ( U i^i A ) e. Univ ) ) $=
      ( vu cgru wcel wtr cv wral wi w3a wa wss ssralv ax-mp elin ral2imi syl2im
      simplbi2 cpw cpr wf crn cuni wal cin wceq ineq1 eleq1d imbi2d cmap elgrug
      co ibi trin inss1 inss2 df-3an im2anan9 visset mapss mp2b inex1 elmap fss
      ex mpan2 sylbi imim1i alimi df-ral sylibr 3impa syl5bi syl6ibr syl cvv wb
      vtoclga com12 ) DFGCHZAIZUAZCGZWCBIZUBZCGZBCJZWCCWFUCZWFUDUEZCGZKZBUFZLZA
      CJZMZDCUGZFGZWQEIZCUGZFGZKWQWSKEDFWTDUHZXBWSWQXCXAWRFWTDCUIUJUKWTFGZWQXAH
      ZWDXAGZWGXAGZBXAJZWKXAGZBXAWCULUNZJZLZAXAJZMZXBXDWTHZWDWTGZWGWTGZBWTJZWKW
      TGZBWTWCULUNZJZLZAWTJZMZWQXNKXDYDABWTFUMUOXOWBXEYCWPXMXOWBXEWTCUPVGYCYBAX
      AJZWPWOAXAJZXMXAWTNZYCYEKWTCUQZYBAXAWTOPXACNZWPYFKWTCURZWOAXACOPYBWOXLAXA
      YBWOXFXHMZXKMZXLWOWEWIMZWNMZYBYLWEWIWNUSXPXRYAYNYLKXPXRMYMYKYAWNXKXPWEXFX
      RWIXHXFXPWEWDWTCQTXRXQBXAJZWIWHBXAJZXHYGXRYOKYHXQBXAWTOPYIWIYPKYJWHBXACOP
      XQWHXGBXAXGXQWHWGWTCQTRSUTYAXSBXJJZWNWLBXJJZXKYGXJXTNYAYQKYHXAWTWCEVAZAVA
      ZVBXSBXJXTOVCWNWFXJGZWLKZBUFYRWMUUBBUUAWJWLUUAWCXAWFUCZWJXAWCWFWTCYSVDZYT
      VEUUCYIWJYJWCXACWFVFVHVIVJVKWLBXJVLVMXSWLXIBXJXIXSWLWKWTCQTRSUTVNVOXFXHXK
      USVPRSUTVQXAVRGXBXNVSUUDABXAVRUMPVPVTWA $.
      $( [10-Jun-2013] $)

    $( The wellfounded part of a universe is another universe.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    wfgru $p |- ( U e. Univ -> ( U i^i U. ( R1 " On ) ) e. Univ ) $=
      ( vx vy cr1 con0 cima cuni wtr cv cpw wcel cpr wral wf wi cgru wss visset
      rgen r1elss crn wal w3a cin r1elssi dftr3 pwwf wa prss prex bitr4i biimpi
      mpbir r19.21aiva frn rnex uniwf sylbir syl ax-gen a1i 3jca ingru mp2an )
      DEFGZHZBIZJVEKZVGCIZLZVEKZCVEMZVGVEVINZVIUAZGVEKZOZCUBZUCZBVEMAPKAVEUDPKO
      VFVGVEQZBVEMVSBVEVGUESBVEUFUMVRBVEVGVEKZVHVLVQVGUGVTVKCVEVTVIVEKUHZVKWAVJ
      VEQVKVGVIVEBRCRZUIVJVGVIUJTUKULUNVQVTVPCVMVNVEQZVOVGVEVIUOWCVNVEKVOVNVIWB
      UPTVNUQURUSUTVAVBSBCVEAVCVD $.
      $( [17-Jun-2013] $)

    $d B x y $.  $d U x y $.
    $( Each ordinal that is comparable with an element of the universe is in
       the universe.  (Contributed by Mario Carneiro, 10-Jun-2013.) $)
    grudomon $p |- ( ( U e. Univ /\ A e. On /\ ( B e. U /\ A ~<_ B ) ) ->
        A e. U ) $=
      ( vx vy wcel cdom wbr wa con0 wi wceq breq1 eleq1 imbi12d imbi2d wral wss
      cv syl r19.21v w3a simpl1 cvv visset onelss imp ssdomg mpsyl sylan simplr
      domtr syl11anc pm2.27 ralimdva dfss3 cen wex wb simp3 domeng biimpa gruss
      cgru 3expia 3adant1 adantr ensym a1i anim12d ancomsd eximdv simpl2 3com23
      gruen 3exp 19.23adv syld mpd syl5bir ex com23 3expib a2d tfis3 com3l impr
      syl5bi 3impia ) CVDFZBCFZABGHZIZAJFZACFZWJWMWNWOWJWKWLWNWOKWNWJWKIZWLWOWP
      DSZBGHZWQCFZKZKZWPESZBGHZXBCFZKZKZWPWLWOKZKDEAWQXBLZWTXEWPXHWRXCWSXDWQXBB
      GMWQXBCNOPWQALZWTXGWPXIWRWLWSWOWQABGMWQACNOPXFEWQQWPXEEWQQZKWQJFZXAWPXEEW
      QUAXKWPXJWTXKWJWKXJWTKXKWJWKUBZWRXJWSXLWRXJWSKXLWRIZXJXDEWQQZWSXMXEXDEWQX
      MXBWQFZIZXCXEXDKXPXBWQGHZWRXCXMXKXOXQXKWJWKWRUCXBUDFXKXOIXBWQRZXQEUEZXKXO
      XRWQXBUFUGXBWQUDUHUIUJXLWRXOUKXBWQBULUMXCXDUNTUOXNWQCRZXMWSEWQCUPXMWQXBUQ
      HZXBBRZIZEURZXTWSKZXLWRYDXLWKWRYDUSXKWJWKUTEWQBCVATVBXMYDXDXBWQUQHZIZEURZ
      YEXMYCYGEXMYBYAYGXMYBXDYAYFXLYBXDKZWRWJWKYIXKWJWKYBXDBXBCVCVEVFVGYAYFKXMW
      QXBXSVHVIVJVKVLXMWJYHYEKXKWJWKWRVMWJYGYEEWJYGXTWSWJXTYGWSWQXBCVOVNVPVQTVR
      VSVTVRWAWBWCWDWHWEWFWGWIVN $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d U x y $.
    gruina.1 $e |- A = ( U i^i On ) $.
    $( If a Grothendieck's universe ` U ` is nonempty, then the height of the
       ordinals in ` U ` is a strongly inaccessible cardinal.  (Contributed by
       Mario Carneiro, 17-Jun-2013.) $)
    gruina $p |- ( ( U e. Univ /\ U =/= (/) ) -> A e. Inacc ) $=
      ( vx vy wcel c0 wne wa cfv wceq csdm wbr wral wex con0 wss syl adantr cen
      cgru ccf cv cpw cina wi cin 0ss gruss mp3an3 0elon jctir sylibr syl6eleqr
      n0 elin ne0i expcom 19.23aiv sylbi impcom word cvv wtr cep wwe r19.21aiva
      wn grutr dftr3 tron trin sylancl inss2 epweon wess df-ord sylanbrc inex1g
      mp2 a1i elon2 syl5eqel eloni ordirr biimpri mtod ccrd cuni cint wlim wrex
      cab com inss1 eqsstri sseli cdom visset cardid ensymi grupw syldan cardon
      pwex endom ax-mp grudomon mp3an2 mpanr2 onelss sylc mpsyl endomtr sylancr
      ssdomg canth2 sdomdomtr sylan2 inawinalem winainflem syl111anc wb cardlim
      mpani sdomtr iscard sseq2 limeq bibi12d mpbii mpbid syl11anc eleq1 mpbiri
      cflm abssi eqeltrd w3a mpd fvex syl6eqelr intex onint eqeq1 anbi1d exbidv
      sylib simp2rr simp1l simp2rl syl6ss 3ad2ant3 simp2l breq1 gruen syl112anc
      elab gruuni 3exp 19.23adv wo cfon cfle onsseleq mpan ord elina ) BUAFZBGH
      ZIZAGHZAUBJZAKZDUCZUDZALMZDANZAUEFZUVJUVIUVLUVJUVOBFZDOUVIUVLUFZDBUOUVTUW
      ADUVIUVTUVLUVIUVTIZGAFUVLUWBGBPUGZAUWBGBFZGPFZIGUWCFUWBUWDUWEUVIUVTGUVOQU
      WDUVOUHUVOGBUIUJUKULGBPUPUMCUNAGUQRURUSUTVAZUVKUVMAFZVHZUVNUVKUWGABFZUVKA
      PFZUWIVHUVIUWJUVJUVIAUWCPCUVIUWCVBZUWCVCFUWCPFUVIUWCVDZUWCVEVFZUWKUVIBVDZ
      PVDUWLUVIUVOBQZDBNUWNUVIUWODBUVOBVIVGDBVJUMVKBPVLVMUWMUVIUWCPQPVEVFUWMBPV
      NVOUWCPVEVPVTWAUWCVQVRBPUAVSUWCWBVRWCZSZUWJUWIAAFZUWJAVBUWRVHAWDAWERUWIUW
      JUWRUWIUWJIZAUWCAAUWCFUWSABPUPWFCUNURWGRUVKUVMEUCZWHJZKZUWTAQZAUWTWIZKZIZ
      IZEOZUWGUWIUFZUVKUVMUVOUXAKZUXFIZEOZDWMZFUXHUVKUVMUXMWJZUXMUVKUWJAWKZUVMU
      XNKUWQUVKWNAQZUXOUVKUVLUWJUVOUWTLMEAWLDANZUXPUWFUWQUVIUXQUVJUVIUWJUVRUXQU
      WPUVIUVQDAUVOAFZUVIUVTUVQABUVOAUWCBCBPWOWPZWQUWBUWJUVPUDZAWRMZUVQUVIUWJUV
      TUWPSUWBUXTUXTWHJZTMUYBAWRMZUYAUYBUXTUVPUVODWSZXEZXEUXTWTZXAUVIUVTUXTBFZU
      YCUVIUVTUVPBFUYGUVOBXBUVPBXBXCUYBPFZUVIUYGIZUYBAQZUYCUXTXDZUYIUWJUYBAFZUY
      JUVIUWJUYGUWPSUYIUYBBFZUYHUYLUVIUYGUYBUXTWRMZUYMUYBUXTTMUYNUYFUYBUXTXFXGU
      VIUYHUYGUYNIUYMUYKUYBUXTBXHXIXJUYKUYMUYHIZUYBUWCAUYBUWCFUYOUYBBPUPWFCUNVM
      AUYBXKXLUYBAPXPXMXCUXTUYBAXNXOUWJUVPUXTLMUYAUVQUVPUYEXQUVPUXTAPXRYEXLXSZV
      GZDEAXTXLSDEAYAYBUVIUXPUXOYCZUVJUVIAWHJZAKZUYRUVIUWJUVOALMZDANUYTUWPUVIVU
      ADAUVIUXRIUVOUVPLMUVQVUAUVOUYDXQUYPUVOUVPAYFXOVGDAYGVRUYTWNUYSQZUYSWKZYCU
      YRAYDUYTVUBUXPVUCUXOUYSAWNYHUYSAYIYJYKRSYLDEAPYPYMZUVKUXMPQUXMGHZUXNUXMFU
      XLDPUXKUVOPFZEUXJVUFUXFUXJVUFUXAPFUWTXDUVOUXAPYNYOSUSYQUVKUXNVCFVUEUVKUXN
      UVMVCVUDAUBUUAZUUBUXMUUCUMUXMUUDXOYRUXLUXHDUVMVUGUVOUVMKZUXKUXGEVUHUXJUXB
      UXFUVOUVMUXAUUEUUFUUGUURUUHUVKUXGUXIEUVKUXGUWGUWIUVKUXGUWGYSZAUXDBUXCUXEU
      XBUVKUWGUUIVUIUVIUWTBFZUXDBFUVIUVJUXGUWGUUJZVUIUVIUWTBQUVMBFZUVMUWTTMZVUJ
      VUKVUIUWTABUXCUXEUXBUVKUWGUUKUXSUULUWGUVKVULUXGABUVMUXSWQUUMVUIUXBVUMUVKU
      XBUXFUWGUUNUXBVUMUXAUWTTMUWTWTUVMUXAUWTTUUOYORUWTUVMBUUPUUQUWTBUUSYMYRUUT
      UVAYTWGUVKUWJUWHUVNUFUWQUWJUWGUVNUVMPFZUWJUWGUVNUVBZAUVCVUNUWJIUVMAQVUOAU
      VDUVMAUVEYKUVFUVGRYTUVIUVRUVJUYQSUVSUVLUVNUVRYSDAUVHWFYB $.
      $( [17-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 1.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    grur1a $p |- ( U e. Univ -> ( R1 ` A ) C_ U ) $=
      ( vx vy wcel cr1 cfv wss c0 wceq wi con0 fveq2 3syl wa sseli eleq1 eleq1d
      wral cgru cin inss1 eqsstri sseq2 mpbii ss0 0ss r10 syl6eq sseq1d a1i wne
      mpbiri cv ciun cina cwina gruina inawina wlim winaon r1lim syl11anc inss2
      winalim csuc imbi12d simpr elelsuc word ne0i syl sylan2 eloni ordsucelsuc
      wb syl5ibr mpd imim1i com12 cpw grupw ex adantr r1suc biimprcd syl6 com23
      syld com4r ontr1 pm2.27 exp3a com3r imp ralimdva gruiun 3expia cvv visset
      biimprd sylan9r exp32 com34 tfinds2 impcom grutr syldan r19.21aiva sylibr
      sylc mpan iunss eqsstrd pm2.61dne ) BUAFZAGHZBIZBJBJKZXSLXQXTAJIZAJKZXSXT
      ABIYAABMUBZBCBMUCUDZBJAUEUFAUGYBXSJBIBUHYBXRJBYBXRJGHZJAJGNUIUJUKUNOULXQB
      JUMZXSXQYFPZXRDADUOZGHZUPZBYGAUQFZAURFZXRYJKZABCUSZAUTZYLAMFZAVAYMAVBZAVF
      DAMVCVDOXQYJBIZYFXQYIBIZDATYRXQYSDAXQYHAFZYIBFZYSYTXQUUAYTYHMFZXQUUALAMYH
      AYCMCBMVEUDQUUBXQYTUUAYTUUALJAFZJBFZLZEUOZAFZUUFGHZBFZLZUUFVGZAFZUUKGHZBF
      ZLXQDEYHJKZYTUUCUUAUUDYHJARUUOYIJBUUOYIYEJYHJGNUIUJSVHYHUUFKZYTUUGUUAUUIY
      HUUFARUUPYIUUHBYHUUFGNSVHYHUUKKZYTUULUUAUUNYHUUKARUUQYIUUMBYHUUKGNSVHUUEX
      QABJYDQULXQUUJUULUUFMFZUUNXQUULUUJUURUUNLZXQUULUUJUUSLXQUULPZUUJUUIUUSUUJ
      UUTUUIUUTUUGUUIUUTUULUUGXQUULVIUULUUGUUTUUKAVGFZUUKAVJUUTYPAVKUUGUVAVQUUL
      XQYFYPUULUUKBFYFABUUKYDQBUUKVLVMYGYKYLYPYNYOYQOZVNAVOUUFAVPOVRVSVTWAUUTUU
      IUUHWBZBFZUUSXQUUIUVDLUULXQUUIUVDUUHBWCWDWEUURUUNUVDUURUUMUVCBUUFWFSWGWHW
      JWDWIWKYHVAZXQYTUUJEYHTZUUAUVEXQYTUVFUUALXQYTPZUVFEYHUUHUPZBFZUVEUUAUVGUV
      FUUIEYHTZUVIUVGUUJUUIEYHUVGUUFYHFZUUJUUILZUVGYTYPUVKUVLLXQYTVIYTXQYFYPYTY
      HBFZYFABYHYDQZBYHVLVMUVBVNYPUVKYTUVLYPUVKYTUVLYPUVKYTPUUGUVLUUFYHAWLUUGUU
      IWMWHWNWOXLWPWQYTXQUVMUVJUVILUVNXQUVMUVJUVIEYHUUHBWRWSVNWJUVEUUAUVIUVEYIU
      VHBYHWTFUVEYIUVHKDXAEYHWTVCXMSXBXCXDXEXFWOVSXGYIBXHXIXJDAYIBXNXKWEXOWDXP
      $.
      $( [23-Jun-2013] $)

    $( A characterization of Grothendieck's universes, part 2.  (Contributed by
       Mario Carneiro, 24-Jun-2013.) $)
    grur1 $p |- ( ( U e. Univ /\ U e. U. ( R1 " On ) ) -> U = ( R1 ` A ) ) $=
      ( vy vx wcel con0 wa cfv wss wn crnk ad2ant2r syl11anc wi syl cvv wbr ccf
      wceq cgru cr1 cima cuni cv wrex wex nss wo simprr wb simplr r1elssi sseld
      simprl sylc cina cwina c0 ne0i gruina sylan2 inawina winaon 3syl rankr1ag
      wne mtbid w3o rankon word eloni ordtri3or syl2an sylancr 3orass sylib ord
      mpd fveq2 eqeq1d rcla4ev ad2antrl ctc tcrank eleq2d grutr wral r19.21aiva
      ex dftr3 sylibr adantr visset tcmin ax-mp wfun wf rankf ffun fvelima mpan
      ssrexv syl2im sylbid jaod 19.23adv syl5bi cdom simpll ad2antll csdm elina
      wtr cpw w3a simp2 sylbi eqtrd rankcf fvex domtri mp2an syl6eqbrr grudomon
      mpbir syl112anc cin biimpri syl6eleqr ordirr adantl pm2.65i pm2.21i exp32
      elin r19.23adv syld pm2.18d grur1a eqssd ) BUAFZBUBGUCUDZFZHZBAUBIZUUEBUU
      FJZUUEUUGKZDUEZLIZATZDBUFZUUGUUHEUEZBFZUUMUUFFZKZHZEUGUUEUULEBUUFUHUUEUUQ
      UULEUUEUUQUULUUEUUQHZUUMLIZATZAUUSFZUIZUULUURUUSAFZKZUVBUURUUOUVCUUEUUNUU
      PUJUURUUMUUCFZAGFZUUOUVCUKUURUUDUUNUVEUUBUUDUUQULUUEUUNUUPUOUUDBUUCUUMBUM
      UNUPZUUBUUNUVFUUDUUPUUBUUNHZAUQFZAURFZUVFUUNUUBBUSVGZUVIBUUMUTABCVAZVBAVC
      ZAVDZVEZMUUMAVFNVHUUBUUNUVDUVBOUUDUUPUVHUVCUVBUVHUVCUUTUVAVIZUVCUVBUIUVHU
      USGFZUVFUVPUUMVJUVOUVQUUSVKAVKZUVPUVFUUSVLAVLZUUSAVMVNVOUVCUUTUVAVPVQVRMV
      SUURUUTUULUVAUUNUUTUULOUUEUUPUUNUUTUULUUKUUTDUUMBUUIUUMTUUJUUSAUUIUUMLVTW
      AWBWJWCUURUVAALUUMWDIZUCZFZUULUURUUSUWAAUURUVEUUSUWATUVGUUMWEPWFUUBUUNUWB
      UULOUUDUUPUVHUVTBJZUWBUUKDUVTUFZUULUVHUUMBJZBXNZUWCUUMBWGZUUBUWFUUNUUBUWE
      EBWHUWFUUBUWEEBUWGWIEBWKWLWMUUMQFUWEUWFHUWCOEWNUUMBQWOWPNLWQZUWBUWDUUCGLW
      RUWHWSUUCGLWTWPDAUVTLXAXBUUKDUVTBXCXDMXEXFVSWJXGXHUUEUUKUUGDBUUEUUIBFZUUK
      UUGUUEUWIUUKHZHZABFZUVFUUGUWKUUBUVFUWIAUUIXIRUWLUUBUUDUWJXJUWKUVIUVJUVFUU
      BUWIUVIUUDUUKUWIUUBUVKUVIBUUIUTUVLVBMZUVMUVNVEZUUEUWIUUKUOUWKAUUJSIZUUIXI
      UWKUWOASIZAUUKUWOUWPTUUEUWIUUJASVTXKUWKUVIUWPATZUWMUVIAUSVGZUWQUUMXOAXLRE
      AWHZXPUWQEAXMUWRUWQUWSXQXRPXSUWOUUIXIRZUUIUWOXLRKZUUIXTUWOQFUUIQFUWTUXAUK
      UUJSYADWNUWOUUIQQYBYCYFYDAUUIBYEYGUWNUWLUVFHZUUGUXBAAFZUXBABGYHZAAUXDFUXB
      ABGYPYICYJUVFUXCKZUWLUVFUVRUXEUVSAYKPYLYMYNNYOYQYRYSUUBUUFBJUUDABCYTWMUUA
      $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d T x y $.
    $( Grothendieck's universes are the same as transitive Tarski's classes,
       part one: a transitive Tarski class is a universe.  (The hard work is in
       ~ tskuni .)  (Contributed by Mario Carneiro, 17-Jun-2013.) $)
    grutsk1 $p |- ( ( T e. Tarski /\ Tr T ) -> T e. Univ ) $=
      ( vx vy ctsk wcel wtr wa cgru cv cpw cpr wral crn cuni cmap co r19.21aiva
      w3a adantlr wb simpr tskpw tskpr wf elmapg tskurn 3expia sylbid r19.21aiv
      3expa 3jca jca elgrug adantr mpbird ) ADEZAFZGZAHEZUQBIZJAEZUTCIZKAEZCALZ
      VBMNAEZCAUTOPZLZRZBALZGZURUQVIUPUQUAURVHBAURUTAEZGZVAVDVGUPVKVAUQUTAUBSUP
      VKVDUQUPVKGVCCAUPVKVBAEVCUTVBAUCUJQSVLVECVFVLVBVFEZUTAVBUDZVEUPVKVMVNTUQA
      UTVBDAUESURVKVNVEUTAVBUFUGUHUIUKQULUPUSVJTUQBCADUMUNUO $.
      $( [17-Jun-2013] $)
  $}

  ${
    $d x y z $.
    $( Grothendieck's universes are the same as transitive Tarski's classes.
       (The proof in the forward direction requires Foundation.)  (Contributed
       by Mario Carneiro, 24-Jun-2013.) $)
    grutsk $p |- Univ = { x e. Tarski | Tr x } $=
      ( vy vz cgru cv wtr ctsk crab wcel wa c0 wceq 0tsk eleq1 mpbiri con0 cuni
      wi cr1 wral a1i wne cin cfv cima visset unir1 eleqtrri grur1 mpan2 adantr
      cvv eqid cina gruina inatsk syl eqeltrd ex pm2.61dne cpw cpr crn cmap w3a
      co elgrug ibi simpld jca grutsk1 impbii treq elrab bitr4i eqriv ) BDAEZFZ
      AGHZBEZDIZVTGIZVTFZJZVTVSIWAWDWAWBWCWAWBVTKVTKLZWBRWAWEWBKGIMVTKGNOUAWAVT
      KUBZWBWAWFJZVTVTPUCZSUDZGWAVTWILZWFWAVTSPUEQZIWJVTULWKBUFUGUHWHVTWHUMZUIU
      JUKWGWHUNIWIGIWHVTWLUOWHUPUQURUSUTWAWCVQVAVTIVQCEZVBVTICVTTWMVCQVTICVTVQV
      DVFTVEAVTTZWAWCWNJACVTDVGVHVIVJVTVKVLVRWCAVTGVQVTVMVNVOVP $.
      $( [24-Jun-2013] $)
  $}

$( I decided to comment out the Card class for now.  We have too many
   definitions, and this one doesn't really buy us much. $)

$(
  @c Card @. @( Class of all cardinal numbers @)

  @( Extend class definition to include the class of all cardinal numbers. @)
  ccdn @a class Card @.

  @( Define the class of all cardinal numbers.  The notation "Card" is used
     in Exercise 5(G) of [JustWeese] p. 174.  It should not be confused with
     the lower-case "card" for the cardinal number function ~ df-card .  @)
  df-cardn @a |- Card = ( om u. ran aleph ) @.

  @( Membership in the class of cardinal numbers. @)
  elcard @p |- ( A e. Card <-> ( card ` A ) = A ) @=
    ( ccdn wcel com cale crn cun ccrd cfv wceq df-cardn eleq2i iscard3 bitr4i )
    ABCADEFGZCAHIAJBOAKLAMN @.
    @( [31-Dec-2004] @) @( [23-Sep-2004] @)

  @( The cardinality of a set is a cardinal number. @)
  cardel @p |- ( card ` A ) e. Card @=
    ( ccrd cfv ccdn wcel wceq cardidm elcard mpbir ) ABCZDEJBCJFAGJHI @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( A natural number is a cardinal number. @)
  nncard @p |- ( A e. om -> A e. Card ) @=
    ( com wcel ccrd cfv wceq ccdn cardnn elcard sylibr ) ABCADEAFAGCAHAIJ @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( An alternate definition of the class of all cardinal numbers. @)
  cardnum2 @p |- Card = { x | ( card ` x ) = x } @=
    ( ccdn com cale crn cun cv ccrd cfv wceq cab df-cardn wcel iscard3 bicomi
    abbi2i eqcomi eqtr4i ) BCDEFZAGZHITJZAKZLSUBUAASUATSMTNOPQR @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of cardinal numbers is a proper class.  Exercise 5(G)(b) of
     [JustWeese] p. 174. @)
  cardnprc @p |- -. Card e. _V @=
    ( vx ccdn cvv wcel cv ccrd cfv wceq cab cardprc cardnum2 eleq1i mtbir ) BCD
    AEZFGNHAIZCDAJBOCAKLM @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of transfinite cardinals (the range of the aleph function) is a
     proper class. @)
  alephprc2 @p |- -. ran aleph e. _V @=
    ( cale crn cvv wcel com cun ccdn cardnprc df-cardn eleq1i mtbi omex wa
    unexb biimpi mpan mto ) ABZCDZERFZCDZGCDUAHGTCIJKECDZSUALUBSMUAERNOPQ @.
    @( [19-Feb-2005] @) @( [23-Sep-2004] @)
$)


