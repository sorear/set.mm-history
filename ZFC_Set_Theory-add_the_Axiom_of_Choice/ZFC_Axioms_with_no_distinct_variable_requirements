$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         ZFC Axioms with no distinct variable requirements
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd1 $p |- ( A. x x = y -> -. A. x y e. z ) $=
    ( cv wceq wal wcel elirrv wsbc stdpc4 pm2.21i elequ1 sbie sylib mto ax-10
    mtoi ) ADBDZEAFRCDZGZAFTBFZUASSGZCHZUATBSIUBTBCJTUBBCUBUBBFUCKBCCLMNOTABPQ
    $.
    $( [1-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd2 $p |- ( A. x x = y -> -. A. x z e. y ) $=
    ( cv wceq wal wcel elirrv wsbc stdpc4 pm2.21i elequ2 sbie sylib mto ax-10
    mtoi ) ADBDZEAFCDZRGZAFTBFZUASSGZCHZUATBSIUBTBCJTUBBCUBUBBFUCKBCCLMNOTABPQ
    $.
    $( [1-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd3 $p |- ( A. x x = y -> -. A. z x e. y ) $=
    ( cv wceq wal wcel wn ax-4 elirrv elequ2 mtbii con3i 3syl ) ADZBDZEZAFQOPGZ
    HRCFZHQAIQOOGRAJABAKLSRRCIMN $.
    $( [2-Jan-2002] $)

  $( A lemma for proving conditionless ZFC axioms. $)
  nd4 $p |- ( A. x x = y -> -. A. z y e. x ) $=
    ( cv wcel wal wn nd3 alequcoms ) BDADECFGBABACHI $.
    $( [2-Jan-2002] $)

  ${
    $d z x $.
    $( A lemma for proving conditionless ZFC axioms. $)
    nd5 $p |- ( -. A. y y = x -> ( z = y -> A. x z = y ) ) $=
      ( cv wceq wal wi dveeq2 nalequcoms ) CDBDEZJAFGABABCHI $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x v w $.  $d y v w $.  $d z v w $.
    $( A version of the Axiom of Extensionality with no distinct variable
       conditions. $)
    axextnd $p |- E. x ( ( x e. y <-> x e. z ) -> y = z ) $=
      ( vw cv wcel wb wceq wal wex wi wn wa hbnae hban dveel2 adantr adantl
      hbbid elequ1 bibi12d a1i cbvald zfext2 syl6bir 19.8a syl6 ex a9e hbae
      ax-8 a4s 19.22d mpi a1d equcomi pm2.61ii 19.35ri ) AEZBEZFZUSCEZFZGZUTVBH
      ZAUSUTHZAIZUSVBHZAIZVDAIZVEAJZKZVGLZVILZVLVMVNMZVJVEVKVOVJDEZUTFZVPVBFZGZ
      DIVEVOVSVDDAVMVNAABANACANOZVOVQVRAVTVMVQVQAIKVNABDPQVNVRVRAIKVMACDPRSVPUS
      HZVSVDGKVOWAVQVAVRVCDABTDACTUAUBUCBCDUDUEVEAUFUGUHVGVKVJVGVHAJVKACUIVGVHV
      EAABAUJVFVHVEKAABCUKULUMUNUOVIVKVJVIVFAJVKABUIVIVFVEAACAUJVHVFVEKAVHVFVBU
      THVEACBUKCBUPUGULUMUNUOUQUR $.
      $( [14-Aug-2003] $)
  $}

  ${
    $d x z w v $.  $d x y w v $.  $d w ph $.
    $( Lemma for the Axiom of Replacement with no distinct variable
       conditions. $)
    axrepndlem1 $p |- ( -. A. y y = z -> E. x ( E. y A. z ( ph -> z = y ) ->
                 A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) ) $=
      ( vw cv wceq wal wn wsbc wi wex wcel wa wb axrep2 hbnae ax-17 hbsb3 a1i
      nd5 hbimd sbequ12r equequ1 imbi12d cbvald exbid dveel2 nalequcoms hbal
      hband hbexd hbbid elequ1 adantl dveeq2 imp hba1 a4s albid syl anbi2d
      exbidv bibi12d ex mpbii ) CFZDFZGCHIZADEFZJZVJVGGZKZEHZCLZVJBFZMZVPVGMZVK
      CHZNZBLZOZEHZKZBLAVHVGGZKZDHZCLZVHVPMZVRACHZNZBLZOZDHZKZBLVKBCEPVIWDWOBCD
      BQZVIVOWHWCWNVIVNWGCCDCQVIVMWFEDCDDQZVIVKVLDWQVKVKDHKVIADEAERSZTDCEUAUBVJ
      VHGZVMWFOKVIWSVKAVLWEAEDUCZEDCUDUETUFUGVIWBWMEDWQVIVQWADWQVQVQDHKVIVQDRTV
      IVTDBWPVIVRVSDVRVRDHKDCDCBUHUIVSVSDHKVIVKDCWRUJTUKULUMVIWSWBWMOVIWSNZVQWI
      WAWLWSVQWIOVIEDBUNUOXAVTWKBXAVSWJVRXAWSCHZVSWJOVIWSXBCDEUPUQXBVKACWSCURWS
      VKAOCWTUSUTVAVBVCVDVEUFUEUGVF $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.  $d w ph $.
    $( Lemma for the Axiom of Replacement with no distinct variable
       conditions. $)
    axrepndlem2 $p |- ( ( ( -. A. x x = y /\ -. A. x x = z ) /\ -. A. y y = z )
                 -> E. x ( E. y A. z ( ph -> z = y ) ->
                     A. z ( z e. x <-> E. x ( x e. y /\ A. y ph ) ) ) ) $=
      ( vw cv wceq wal wn wa wi wex wcel wb wsbc hbnae hban ax-17 hbsb3 a1i
      ax-12 impcom hbimd hbald hbexd dveel1 adantl dveel2 adantr hband hbbid
      nd5 imdistani hba1 sbequ12r imbi1d a4s albid syl pm3.27 ax-4 syl2an
      exbid elequ2 elequ1 sbal2 nalequcoms bicomd sylan9bb adantlr anbi12d ex
      cbvexd bibi12d sylan2 imbi12d axrepndlem1 syl5bi imp ) BFZCFZGBHIZVTDFZGB
      HIZJZWAWCGCHIZAWCWAGZKZDHZCLZWCVTMZVTWAMZACHZJZBLZNZDHZKZBLZWEABEFZOZWGKZ
      DHZCLZWCWTMZWTWAMZXACHZJZELZNZDHZKZELWSWFWEXLWREBWBWDBBCBPBDBPQZWEXDXKBXM
      WEXCBCWBWDCBCCPBDCPQZWEXBBDWBWDDBCDPBDDPZQZWEXAWGBXMXAXABHKWEABEAERSTZWDW
      BWGWGBHKDCBUAUBUCUDUEWEXJBDXPWEXEXIBXMWDXEXEBHKWBBDEUFUGWEXHBEWEERWEXFXGB
      WBXFXFBHKWDBCEUHUIWEXABCXNXQUDUJZUEUKUDUCWEWTVTGZXLWRNWEXSJZXDWJXKWQXTWEX
      SCHZJZXDWJNWEXSYAWBXSYAKWDCBEULUIUMYBXCWICWEYACXNXSCUNQWDXSXCWINZWEYAWDXS
      JWDXSDHZJZYCWDXSYDDBEULZUMYEXBWHDWDYDDXOXSDUNZQYDXBWHNZWDXSYHDXSXAAWGAEBU
      OUPUQUGURUSWBWDUTXSCVAVBVCUSXTWEYDJZXKWQNWEXSYDWDXSYDKWBYFUGUMYIXJWPDWEYD
      DXPYGQWEXSXJWPNYDXTXEWKXIWOXSXEWKNWEEBDVDUGWEXIWONXSWEXHWNEBXMXRWEXSXHWNN
      XTXFWLXGWMXSXFWLNWEEBCVEUGWBXSXGWMNWDWBXGWMBWTOZXSWMWBYJXGYJXGNCBACBEVFVG
      VHWMEBUOVIVJVKVLVMUIVNXSDVAVOURUSVPVLVMXAECDVQVRVS $.
      $( [2-Jan-2002] $)
  $}

  $( A version of the Axiom of Replacement with no distinct variable
     conditions. $)
  axrepnd $p |- E. x ( E. y A. z ( ph -> z = y ) ->
               A. z ( A. y z e. x <-> E. x ( A. z x e. y /\ A. y ph ) ) ) $=
    ( cv wceq wal wi wex wcel wa wb wn axrepndlem2 hbnae hban ax15 com12
    nalequcoms imp adantlr ax-4 impbid1 alequcom con3i syl2an adantll anbi1d
    exbid bibi12d albid imbi2d mpbid exp31 hbae pm5.21 nd2 alequcoms nd3
    intnanrd nexd sylanc 19.21ai a1d 19.8a syl nd4 nd1 pm2.61iii ) BEZCEZFBGZVJ
    DEZFBGZVKVMFCGZAVMVKFZHDGCIZVMVJJZCGZVJVKJZDGZACGZKZBIZLZDGZHZBIZVLMZVNMZVO
    MZWHWIWJKZWKKZVQVRVTWBKZBIZLZDGZHZBIWHABCDNWMWRWGBWLWKBWIWJBBCBOBDBOPCDBOPZ
    WMWQWFVQWMWPWEDWLWKDWIWJDBCDOBDDOPCDDOPWMVRVSWOWDWMVRVSWIWKVRVSHZWJWIWKWTWK
    WTHCBWKVKVJFCGMWTDBCQRSTUAVRCUBUCWMWNWCBWSWMVTWAWBWMVTWAWJWKVTWAHZWIVMVJFDG
    ZMZVPDGZMZXAWJWKXCXEXABCDQTXBVNDBUDUEXDVODCUDUEUFUGVTDUBUCUHUIUJUKULUIUMUNV
    LWGWHVLWFVQVLWEDBCDUOVSMZWDMZWEVLVSWDUPZXFCBCBDUQURVLWCBBCBUOVLWAWBBCDUSUTV
    AVBVCVDWGBVEZVFVNWGWHVNWFVQVNWEDBDDUOXFXGWEVNXHBDCVGVNWCBBDBUOVNWAWBWAMZDBD
    BCVHURUTVAVBVCVDXIVFVOWGWHVOWFVQVOWEDCDDUOXFXGWEVOXHCDBVHVOWCBCDBUOVOWAWBXJ
    DCDCBUQURUTVAVBVCVDXIVFVI $.
    $( [2-Jan-2002] $)

  ${
    $d x y w $.  $d x z w $.
    $( Lemma for the Axiom of Union with no distinct variable
       conditions. $)
    axunndlem1 $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wceq wal wcel wa wex wi hbae wn en2lp elequ2 anbi2d mtbii a4s
      nexd pm2.21d a5i 19.8a syl axun hbnae ax-17 a1i dveel2 hband hbexd hbimd
      wb elequ1 anbi1d exbidv imbi12d cbvald exbid mpbii pm2.61i ) BEZCEZFZBGZV
      AAEZHZVEVBHZIZAJZVFKZBGZAJZVDVKVLVCVJBVDVIVFVDVHABCALVCVHMBVCVFVEVAHZIVHV
      AVENVCVMVGVFBCAOPQRSTUAVKAUBUCVDMZDEZVEHZVGIZAJZVPKZDGZAJVLADCUDVNVTVKABC
      AUEZVNVSVJDBBCBUEZVNVRVPBWBVNVQBAWAVNVPVGBVPVPBGKVNVPBUFUGZBCAUHUIUJWCUKV
      OVAFZVSVJULKVNWDVRVIVPVFWDVQVHAWDVPVFVGDBAUMZUNUOWEUPUGUQURUSUT $.
      $( [2-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Union with no distinct variable conditions. $)
    axunnd $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wceq wal wcel wa wex wi wn axunndlem1 hbnae hban ax-17 dveel1
      adantr dveel2 adantl hband hbexd hbimd hbald wb nd5 imdistani hba1
      elequ2 elequ1 anbi12d a1i cbvexd a4s imbi12d albid syl ex mpbii hbae
      elirrv pm3.26 syl5bi mtoi nexd pm2.21d 19.21ai 19.8a pm3.27 pm2.61ii ) AE
      ZBEZFZAGZVKCEZFZAGZVLVKHZVKVOHZIZAJZVRKZBGZAJZVNLZVQLZWDWEWFIZVLDEZHZWHVO
      HZIZDJZWIKZBGZDJWDDBCMWGWNWCDAWEWFAABANACANOZWGWMABWEWFBABBNACBNOZWGWLWIA
      WOWGWKADWGDPWGWIWJAWEWIWIAGKWFABDQRZWFWJWJAGKWEACDSTUAZUBWQUCUDWGWHVKFZWN
      WCUEZWGWSIWGWSBGZIZWTWGWSXAWEWSXAKWFBADUFRUGXBWMWBBWGXABWPWSBUHOXBWLWAWIV
      RWGWLWAUEXAWGWKVTDAWOWRWSWKVTUEKWGWSWIVRWJVSDABUIZDACUJUKULUMRXAWIVRUEZWG
      WSXDBXCUNTUOUPUQURUMUSURVNWCWDVNWBBABBUTVNWAVRVNVTAABAUTVMVTLZAVMVTVLVLHZ
      BVAVMVRXFVTABBUIVRVSVBVCVDUNVEVFVGWCAVHZUQVQWCWDVQWBBACBUTVQWAVRVQVTAACAU
      TVPXEAVPVTVOVOHZCVAVPVSXHVTACCUJVRVSVIVCVDUNVEVFVGXGUQVJ $.
      $( [2-Jan-2002] $)
  $}

  $( Lemma for the Axiom of Power Sets with no distinct variable
     conditions. $)
  axpowndlem1 $p |- ( A. x x = y -> ( -. x = y ->
             E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) $=
    ( cv wceq wn wcel wex wal wi pm2.24 a4s ) ADZBDZEZOFMNGCHMCDGBIJAINMGJBIAHZ
    JAOPKL $.
    $( [4-Jan-2002] $)

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem2 $p |- ( -. A. x x = y -> ( -. A. x x = z -> ( -. x = y ->
         E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) ) $=
      ( vw cv wceq wal wn wcel wex wi wa axpow 19.8a ax-4 imim12i 19.20i
      imim1i 19.22i ax-mp a1i ax-gen hbnae hban dveeq2 hbnd adantr ax-17
      dveel2 hbexd adantl hbald hbimd dveel1 wb equequ1 negbid nd5 imdistani
      hba1 elequ1 a4s exbid syl adantll albid adantlr imbi12d ex cbvald elequ2
      cbvexd mpbii 19.21bi ) AEZBEZFZAGHZVOCEZFAGHZVQHZVOVPIZCJZVOVSIZBGZKZAGZV
      PVOIZKZBGZAJZKZVRVTLZWLAWMDEZVPFZHZWNVPIZCJZWNVSIZBGZKZDGZVPWNIZKZBGZDJZK
      ZDGWLAGXGDXFWPWQWSKZDGZXCKZBGZDJXFDBCMXKXEDXJXDBXBXIXCXAXHDWQWRWTWSWQCNWS
      BOPQRQSTUAUBWMXGWLDAVRVTAABAUCZACAUCUDZWMWPXFAXMVRWPWPAGKVTVRWOAXLABDUEUF
      UGWMXEADWMDUHZWMXDABVRVTBABBUCACBUCUDZWMXBXCAXMWMXAADVRVTDABDUCACDUCUDWMW
      RWTAXMWMWQACVRVTCABCUCACCUCUDVRWQWQAGKVTABDUIUGUJWMWSABXOVTWSWSAGKVRACDUI
      UKULUMZULVRXCXCAGKVTABDUNUGZUMULUJUMWMWNVOFZXGWLUOWMXRLZWPWAXFWKXRWPWAUOW
      MXRWOVQDABUPUQUKWMXFWKUOXRWMXEWJDAXMWMXDABXOWMXBXCAXMWMXAADXNXPULXQUMULWM
      XRXEWJUOZXSWMXRBGZLZXTWMXRYAVRXRYAKVTBADURZUGUSYBXDWIBWMYABXOXRBUTZUDYBXB
      WGXCWHWMXBWGUOYAWMXAWFDAXMXPWMXRXAWFUOXSWRWCWTWEVTXRWRWCUOZVRVTXRLVTXRCGZ
      LYEVTXRYFCADURUSYFYEVTYFWQWBCXRCUTXRWQWBUOCDABVAVBVCUKVDVEVRXRWTWEUOZVTVR
      XRLVRYALYGVRXRYAYCUSYAYGVRYAWSWDBYDXRWSWDUOBDACVAVBVFUKVDVGVHVIVJUGYAXCWH
      UOZWMXRYHBDABVKVBUKVHVFVDVIVLUGVHVIVJVMVNVI $.
      $( [4-Jan-2002] $)
  $}


  ${
    $d x w $.  $d y z w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem3 $p |- ( -. x = y ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) $=
      ( vw cv wceq wal wn wcel wex wi axpowndlem2 axpowndlem1 hbae wb nd3 mtt
      syl ax-10 alequcoms alnex 3imtr3g sylbird a4sd imim1d 19.20d 19.22d c0
      csn p0ex eleq2 imbi2d albidv cla4ev 0ex snid eleq1 mpbiri mpg n0 con1bii
      imbi1i albii exbii mpbir hbnae dveel1 nalequcoms hbexd hbnd dveel2 hbimd
      wa dveeq2 imdistani hba1 elequ2 a4s exbid adantl negbid elequ1 imbi12d
      ex cbvald mpbii syl5 a1dd pm2.61d2 pm2.61ii ) AEZBEZFZAGZWKCEZFAGZWMHZWKW
      LIZCJZWKWOIBGZKZAGZWLWKIZKZBGZAJZKZABCLABCMZWPWNXGWPWNHZXFWQWPWRAJZHZXCKZ
      BGZAJZXFXIWPXMXEAACANWPXLXDBACBNWPXBXKXCWPXAXKAWPXAWSHZXKWPWTHXOXAOACBPWT
      WSQRWPWRHZCGZXPAGZXOXKXQXRKCAXPCASTWRCUAWRAUAUBUCUDUEUFUGXIWKDEZIZAJZHZXS
      WKIZKZDGZAJZXNYFXSUHFZYCKZDGZAJZYGXSUHUIZIZKZYJDYIYMDGAYKUJWKYKFZYHYMDYNY
      CYLYGWKYKXSUKULUMUNYGYLUHYKIUHUOUPXSUHYKUQURUSYEYIAYDYHDYBYGYCYGYAAXSUTVA
      VBVCVDVEXIYEXMAABAVFZXIYDXLDBABBVFZXIYBYCBYPXIYABYPXIXTBAYOXTXTBGKBABADVG
      VHVIVJYCYCBGKBABADVKVHVLXIXSWLFZYDXLOXIYQVMZYBXKYCXCYRYAXJYRXIYQAGZVMYAXJ
      OZXIYQYSABDVNVOYSYTXIYSXTWRAYQAVPYQXTWROADBAVQVRVSVTRWAYQYCXCOXIDBAWBVTWC
      WDWEVSWFWGWHXHWIWJ $.
      $( [4-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( Lemma for the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpowndlem4 $p |- ( -. A. y y = x -> ( -. A. y y = z -> ( -. x = y
      -> E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) ) $=
      ( vw cv wceq wal wn wcel wex wi wa axpowndlem3 ax-gen hbnae hban dveeq1
      hbnd adantr ax-17 dveel1 hbexd ax15 imp hbald hbimd dveel2 wb equequ2
      negbid adantl nd5 imdistani hba1 elequ2 a4s exbid syl ax-4 sylan2
      adantll pm4.2i a1i cbvald imbi12d albid elequ1 ex mpbii 19.21bi ) BEZAEZF
      BGHZVKCEZFBGHZVLVKFZHZVLVKIZCJZVLVNIZBGZKZAGZVKVLIZKZBGZAJZKZVMVOLZWHBWIV
      LDEZFZHZVLWJIZCJZVTDGZKZAGZWJVLIZKZDGZAJZKZDGWHBGXBDADCMNWIXBWHDBVMVOBBAB
      OZBCBOPZWIWLXABXDVMWLWLBGKVOVMWKBXCBADQRSWIWTBAVMVOABAAOBCAOPZWIWSBDWIDTZ
      WIWQWRBXDWIWPBAXEWIWNWOBXDVMWNWNBGKVOVMWMBCBACOBADUAUBSWIVTBDXFVMVOVTWAKA
      CBUCUDZUEUFUEVMWRWRBGKVOBADUGSUFZUEUBUFWIWJVKFZXBWHUHWIXILZWLVQXAWGXIWLVQ
      UHWIXIWKVPDBAUIUJUKWIXAWGUHXIWIWTWFAXEWIWSWEDBXDXHWIXIWSWEUHXJWQWCWRWDXJW
      IXIAGZLZWQWCUHWIXIXKVMXIXKKVOABDULSUMXLWPWBAWIXKAXEXIAUNPXLWNVSWOWAVOXKWN
      VSUHZVMVOXIXMXKVOXILVOXICGZLXMVOXIXNCBDULUMXNXMVOXNWMVRCXICUNXIWMVRUHCDBA
      UOUPUQUKURXIAUSUTVAWIWOWAUHXKWIVTVTDBXDXGXIVTVTUHKWIXIVTVBVCVDSVEVFURXIWR
      WDUHWIDBAVGUKVEVHVDUQSVEVHVDVIVJVH $.
      $( [4-Jan-2002] $)
  $}


  ${
    $d x w $.  $d y w $.
    $( A version of the Axiom of Power Sets with no distinct variable
       conditions. $)
    axpownd $p |- ( -. x = y ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) $=
      ( vw cv wceq wal wn wcel wex wi axpowndlem4 axpowndlem1 alequcoms a1d wa
      hbnae hbae hban el dveel1 nalequcoms wb elequ2 a1i cbvexd mpbii 19.8a
      syl df-ex sylib adantr pm4.2i dral1 alnex 3bitr3g nd2 mtt bitrd dral2
      adantl mtbid pm2.21d 19.21ai ex pm2.61i pm2.61ii ) BEZAEZFBGVHCEZFBGZVIVH
      FZHZVIVHIZCJZVIVJIBGZKZAGZVHVIIZKZBGZAJZKZABCLWCABABCMZNVLAGZVKWCKWEWCVKW
      DOWEHZVKWCWFVKPZWBVMWGWAWBWGVTBWFVKBABBQZBCBRSWGVRVSWGVNBJZHZAGZVRWFWKHZV
      KWFWIAJZWLWFWIWMWFVIDEZIZDJWIADTWFWOVNDBWHWOWOBGKBABADUAUBWNVHFWOVNUCKWFD
      BAUDUEUFUGWIAUHUIWIAUJUKULVKWKVRUCWFWJVQBCAVKWJVOHZVQVKVNHZBGWQCGWJWPWQWQ
      BCVKWQUMUNVNBUOVNCUOUPVKVPHWPVQUCBCAUQVPVOURUIUSUTVAVBVCVDWAAUHUIOVEVFVG
      $.
      $( [4-Jan-2002] $)
  $}

$(
  @{
    @d x z @.  @d y z @.
    axpowndNEWlem1 @p  |- ( A. x x = y ->
        ( ( A. x x = y -> E. z A. x ( E. z z e. y -> x e. z ) )
         -> E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) ) @=
      ( weq wal wel wex wi cv csn c0 wceq wn wcel wa en2lp visset snid biantru
      mtbir snnzOLD a1bi mtbi n0 imbi1i albii bicomi snex eqeq1 negbid eleq1
     imbi12d cla4v sylbi mto hbae wb elequ2 a4s exbidv imbi1d albid mtbii nexdv
     pm2.21d a2i com12 ) ABDZAEZCBFZCGZACFZHZAEZCGZHVIABFCGVLBEHAEBAFHBEAGZVIVO
      VPVIVOVPVIVNCVICAFZCGZVLHZAEZVNVTCIZJZKLZMZWBWANZHZWEWFWEWEWAWBNZOWBWAPWG
      WEWACQZRSTWDWEWAWHUAUBUCVTAIZKLZMZVLHZAEZWFWMVTWLVSAWKVRVLCWIUDUEUFUGWLWF
      AWBWAUHWIWBLZWKWDVLWEWNWJWCWIWBKUIUJWIWBWAUKULUMUNUOVIVSVMAABAUPVIVRVKVLV
      IVQVJCVHVQVJUQAABCURUSUTVAVBVCVDVEVFVG @.
  @}

  @{
    @d x w @.  @d y w @.
   axpowndNEW @p  |- ( ( A. x x = y -> E. z A. x ( E. z z e. y -> x e. z ) ) ->
               E. x A. y ( A. x ( E. z x e. y -> A. y x e. z ) -> y e. x ) ) @=
?@.
  @}
$)

  $( Lemma for the Axiom of Regularity with no distinct variable
     conditions. $)
  axregndlem1 $p |- ( A. x x = z -> ( x e. y ->
               E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) ) $=
    ( cv wceq wal wcel wex wn wi wa hbae elirrv elequ1 mtbii a4s pm2.21d
    19.21ai anim2i expcom 19.22d 19.8a syl5 ) ADZCDZEZAFZUDBDZGZAHUIUEUDGZUEUHG
    IZJZCFZKZAHUIUGUIUNAACALUIUGUNUGUMUIUGULCACCLUGUJUKUFUJIAUFUDUDGUJAMACANOPQ
    RSTUAUIAUBUC $.
    $( [3-Jan-2002] $)

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Regularity with no distinct variable
       conditions. $)
    axregndlem2 $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( vw cv wceq wal wcel wn wi wa wex axreg ax-gen hbnae hban dveel2 adantr
      ax-17 dveel1 adantl ax15 impcom hbnd hbimd hbald hband hbexd wb elequ1
      nd5 imdistani hba1 elequ2 imbi1d a4s albid syl anbi12d ex cbvexd imbi12d
      cbvald mpbii 19.21bi elirrv mtbii pm2.21d axregndlem1 pm2.61ii ) AEZBEZFZ
      AGZVKCEZFAGZVKVLHZVQVOVKHZVOVLHZIZJZCGZKZALZJZVNIZVPIZWEWFWGKZWEAWHDEZVLH
      ZWJVOWIHZVTJZCGZKZDLZJZDGWEAGWPDDBCMNWHWPWEDAWFWGAABAOACAOPZWHWJWOAWQWFWJ
      WJAGJWGABDQRZWHWNADWHDSWHWJWMAWRWHWLACWFWGCABCOACCOZPWHWKVTAWQWGWKWKAGJWF
      ACDTUAWHVSAWQWGWFVSVSAGJCBAUBUCUDUEUFUGZUHUEWHWIVKFZWPWEUIWHXAKWJVQWOWDXA
      WJVQUIZWHDABUJZUAWHWOWDUIXAWHWNWCDAWQWTWGXAWNWCUIZJWFWGXAXDWGXAKZWJVQWMWB
      XAXBWGXCUAXEWGXACGZKZWMWBUIWGXAXFCADUKULXGWLWACWGXFCWSXACUMPXFWLWAUIZWGXA
      XHCXAWKVRVTDACUNUOUPUAUQURUSUTUAVARVBUTVCVDVEUTVNVQWDVMVQIAVMVKVKHVQAVFAB
      AUNVGUPVHABCVIVJ $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Regularity with no distinct variable
       conditions. $)
    axregnd $p |- ( x e. y ->
                 E. x ( x e. y /\ A. z ( z e. x -> -. z e. y ) ) ) $=
      ( vw cv wceq wal wcel wn wi wa wex hbnae hban dveel2 adantr adantl hbnd
      hbimd wb elequ1 negbid imbi12d a1i cbvald anbi2d exbid axregndlem2
      syl5bi ex axregndlem1 alequcoms hbae elirrv elequ2 mtbii a4s a1d a5i
      anim2i expcom 19.22d 19.8a syl5 pm2.61ii ) CEZAEZFCGZVFBEZFZCGZVGVIHZVLVF
      VGHZVFVIHZIZJZCGZKZALZJZVHIZVKIZVTWAWBKZVLDEZVGHZWDVIHZIZJZDGZKZALVSVLWCW
      JVRAWAWBACAAMCBAMNWCWIVQVLWCWHVPDCWAWBCCACMCBCMNZWCWEWGCWKWAWEWECGJWBCADO
      PWCWFCWKWBWFWFCGJWACBDOQRSWDVFFZWHVPTJWCWLWEVMWGVODCAUAWLWFVNDCBUAUBUCUDU
      EUFUGABDUHUIUJVTACABCUKULVKVLALVSVLVKVLVRACBAUMVLVKVRVKVQVLVJVPCVKVOVMVJV
      OCVJVFVFHVNCUNCBCUOUPUQURUSUTVAVBVLAVCVDVE $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x w $.  $d z y w $.
    $( Lemma for the Axiom of Infinity with no distinct variable
       conditions. $)
    axinfndlem1 $p |- ( A. x y e. z -> E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) ) $=
      ( vw cv wceq wal wcel wa wex wi wn axinf a1i hbnae hban ax15 imp wb
      pm4.2i cbvald dveel1 adantr adantl hband hbexd hbimd hbald elequ2 nd5
      imdistani hba1 anbi2d a4s exbid syl imbi12d ax-4 sylan2 albid adantll
      anbi12d ex cbvexd mpbii nd1 pm2.21d nd2 pm2.61ii ) AEZBEZFAGZVJCEZFAGZVKV
      MHZAGZVKVJHZVQVOVMVJHZIZCJZKZBGZIZAJZKZVLLZVNLZWEWFWGIZVODGZVKDEZHZWKVOVM
      WJHZIZCJZKZBGZIZDJZKWEWRWIDBCMNWHWIVPWRWDWHVOVODAWFWGAABAOACAOPZWFWGVOVPK
      BCAQRZWJVJFZVOVOSKWHXAVOTNUAWHWQWCDAWSWHWKWPAWFWKWKAGKWGABDUBUCZWHWOABWFW
      GBABBOACBOZPWHWKWNAWSXBWHWMACWFWGCABCOACCOPWHVOWLAWTWGWLWLAGKWFACDUBUDUEU
      FUGUHUEWHXAWQWCSWHXAIZWKVQWPWBXAWKVQSZWHDABUIZUDXDWHXABGZIWPWBSZWHXAXGWFX
      AXGKWGBADUJUCUKWGXGXHWFWGXGIWOWABWGXGBXCXABULPWGXAWOWASXGWGXAIZWKVQWNVTXA
      XEWGXFUDXIWGXACGZIWNVTSZWGXAXJCADUJUKXJXKWGXJWMVSCXACULXAWMVSSCXAWLVRVODA
      CUIUMUNUOUDUPUQXABURUSUTVAUPVBVCVDUQVEVCVLVPWDABCVFVGVNVPWDACBVHVGVI $.
      $( [5-Jan-2002] $)
  $}

  ${
    $d x w $.  $d y w $.  $d z w $.
    $( A version of the Axiom of Infinity with no distinct variable
       conditions. $)
    axinfnd $p |- E. x ( y e. z -> ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) ) $=
      ( vw cv wcel wa wex wi wal wceq wn axinfndlem1 ax-gen hbnae hban dveel2
      hbald adantl adantr ax-17 ax15 impcom hband hbexd hbimd wb nd5 imdistani
      hba1 elequ1 a4s albid anbi1d exbid syl imbi12d adantll ex cbvald
      bi2anan9r mpbii 19.21bi nd1 alequcoms pm2.21d nd3 pm2.61ii 19.35ri ) BEZC
      EZFZVJAEZFZVNVLVKVMFZGZCHZIZBJZGZAVJVMKBJZVJVKKBJZVLAJZVTAHZIZWALZWBLZWEW
      FWGGZWEBWHDEZVKFZAJZWIVMFZWLWJVOGZCHZIZDJZGZAHZIZDJWEBJWSDADCMNWHWSWEDBWF
      WGBBABOBCBOPZWHWKWRBWTWGWKWKBJIWFWGWJBABCAOZBCDQZRSWHWQBAWFWGABAAOXAPZWHW
      LWPBWFWLWLBJIWGBADQTZWHWOBDWHDUAWHWLWNBWTXDWHWMBCWFWGCBACOBCCOPWHWJVOBWGW
      JWJBJIWFXBSWGWFVOVOBJICABUBUCUDUEUFZRUDUEUFWHWIVJKZWSWEUGZWHXFGWHXFAJZGZX
      GWHXFXHWFXFXHIWGABDUHTUIXIWKWCWRWDXHWKWCUGWHXHWJVLAXFAUJZXFWJVLUGADBCUKZU
      LUMSXIWQVTAWHXHAXCXJPXHWLVNWHWPVSXFWLVNUGZADBAUKZULWHWOVRDBWTXEWHXFWOVRUG
      ZWGXFXNWFWGXFGZWLVNWNVQXFXLWGXMSXOWGXFCJZGWNVQUGZWGXFXPCBDUHUIXPXQWGXPWMV
      PCXFCUJXFWMVPUGCXFWJVLVOXKUNULUOSUPUQURUSUTVAUOUQUPUSUTVBVCUSWAWCWDWCLABA
      BCVDVEVFWBWCWDBCAVGVFVHVI $.
      $( [5-Jan-2002] $)
  $}

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem1 $p |- ( A. x x = y ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb wi hbae nd1 pm2.21d pm3.26 19.20i syl5
    19.21ai 19.8a syl ) AEZBEZFAGZUCCEZHZUEDEZHZIZAGZUIUCUGHUGUBHIIDJUCUGFKBGDJ
    ZLZCGZBGZUNAJUDUMBABBMUDULCABCMUDUFAGZUKUJUDUOUKABCNOUIUFAUFUHPQRSSUNATUA
    $.
    $( [3-Jan-2002] $)

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem2 $p |- ( A. x x = z ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb wi hbae nd1 pm2.21d pm3.27 19.20i syl5
    19.21ai 19.8a syl ) AEZCEZFAGZBEZUCHZUCDEZHZIZAGZUIUEUGHUGUBHIIDJUEUGFKBGDJ
    ZLZCGZBGZUNAJUDUMBACBMUDULCACCMUDUHAGZUKUJUDUOUKACDNOUIUHAUFUHPQRSSUNATUA
    $.
    $( [3-Jan-2002] $)

  $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
  axacndlem3 $p |- ( A. y y = z ->
            E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y ( E. w
          ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) ) $=
    ( cv wceq wal wcel wa wex wb wi hbae nd3 pm2.21d pm3.26 19.20i syl5
    19.21ai a5i 19.8a syl ) BEZCEZFZBGZUCUDHZUDDEZHZIZAGZUJUCUHHUHAEHIIDJUCUHFK
    BGDJZLZCGZBGZUOAJUEUNBUFUMCBCCMUFUGAGZULUKUFUPULBCANOUJUGAUGUIPQRSTUOAUAUB
    $.
    $( [3-Jan-2002] $)

  ${
    $d x v $.  $d y z w v $.
    $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
    axacndlem4 $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) ->
              E. w A. y ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) )
                     <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn axac ax-4 imim1i 19.20i2 19.22i
      ax-mp hbnae hban ax-17 ax15 impcom adantrr imp adantrl hband hbald
      dveel1 adantl hbexd ax-12 hbbid hbimd nd5 sylan9 hba1 hbal pm4.2i a1i
      cbvald adantr 19.20d 19.20i syl5 imdistani elequ2 anbi2d a4s exbid
      bibi1d albid syl adantll imbi12d ex syld cbvexd mpbii exp32 axacndlem2
      axacndlem1 hbae nd2 pm2.21d pm3.27 19.21ai 19.8a pm2.61iii ) AFZCFZGAHZWL
      BFZGAHZWLDFZGAHZWOWMIZWMWQIZJZAHZXAWOWQIZWQWLIZJZJZDKZWOWQGZLZBHZDKZMZCHZ
      BHZAKZWNNZWPNZWRNZXOXPXQXRJZJZXAEHZXAXCWQEFZIZJZJZDKZXHLZBHZDKZMZCHZBHZEK
      ZXOXAYIMZCHBHZEKYMEBCDOYOYLEYNYJBCYAXAYIXAEPQRSTXTYLXNEAXPXSAACAUAXQXRAAB
      AUAADAUAUBUBZXTYKABXPXSBACBUAXQXRBABBUAADBUAZUBUBZXTYJACXPXSCACCUAXQXRCAB
      CUAZADCUAUBUBZXTYAYIAYPXTXAAEXTEUCXTWSWTAXPXQWSWSAHMZXRXQXPUUABCAUDUEUFXP
      XRWTWTAHZMZXQXPXRUUCCDAUDUGUHUIZUJXTYHADXPXSDACDUAXQXRDABDUAADDUAUBUBZXTY
      GABYRXTYFXHAYPXTYEADUUEXTXAYDAUUDXSYDYDAHMXPXSXCYCAXQXRXCXCAHMBDAUDUGXRYC
      YCAHMXQADEUKULUIULUIUMXSXHXHAHMZXPXQXRUUFBDAUNUGULUOUJUMUPUJUJXTYBWLGZUUG
      CHZBHZYLXNLZXPXQUUGUUIMXRXPUUGUUHXQUUICAEUQXQUUGBCYSBAEUQUJURUFXTUUIUUJXT
      UUIJZYKXMBXTUUIBYRUUHBUSUBUUKYJXLCXTUUICYTUUHCBUUGCUSUTUBUUKYAXBYIXKXTYAX
      BLUUIXTXAXAEAYPUUDUUGXAXALMXTUUGXAVAVBVCVDXSUUIYIXKLZXPXSUUIJXSUUGDHZBHZJ
      UULXSUUIUUNXRUUIUUNMXQXRUUGBHUUNUUIXRUUGUUMBYQDAEUQVEUUHUUGBUUGCPVFVGULVH
      UUNUULXSUUNYHXJDUUMDBUUGDUSZUTUUNYGXIBUUMBUSUUMYGXILBUUMYFXGXHUUMYEXFDUUO
      UUGYEXFLDUUGYDXEXAUUGYCXDXCEADVIVJVJVKVLVMVKVNVLULVOVPVQVNVNVRVSVTWAWBABC
      DWCABCDWDWRXNXOWRXMBADBWEWRXLCADCWEWRUUBXKXBWRUUBXKADCWFWGXAWTAWSWTWHVFVG
      WIWIXNAWJVOWK $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x v $.  $d y v $.  $d z w v $.
    $( Lemma for the Axiom of Choice with no distinct variable conditions. $)
    axacndlem5 $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) ->
              E. w A. y ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) )
                     <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn axacndlem4 hbnae hban dveel2
      adantr ax15 imp adantrl hband hbald ax-17 ad2antll impcom adantl hbexd
      dveeq2 hbbid hbimd nd5 imdistani hba1 elequ1 anbi1d a4s albid syl ax-4
      sylan2 adantlr adantll anbi12d exbid equequ1 bibi12d ex cbvald imbi12d
      mpbii exp32 axacndlem3 axacndlem1 alequcoms hbae en2lp elequ2 anbi2d
      mtbii pm2.21d a4sd 19.21ai a5i 19.8a pm2.61iii ) BFZCFZGBHZWHAFZGBHZWHDFZ
      GZBHZWHWIIZWIWMIZJZAHZWRWHWMIZWMWKIZJZJZDKZWNLZBHZDKZMZCHZBHZAKZWJNZWLNZW
      ONZXKXLXMXNJZJZEFZWIIZWQJZAHZXSXQWMIZXAJZJZDKZXQWMGZLZEHZDKZMZCHZEHZAKXKA
      ECDOXPYKXJAXLXOABCAPXMXNABAAPBDAPQQZXPYJXIEBXLXOBBCBPXMXNBBABPBDBPQQZXPYI
      BCXLXOCBCCPXMXNCBACPBDCPQQZXPXTYHBYMXPXSBAYLXPXRWQBXLXRXRBHMXOBCERSXLXNWQ
      WQBHMZXMXLXNYOCDBTUAUBUCZUDXPYGBDXLXODBCDPXMXNDBADPBDDPQQZXPYFBEXPEUEXPYD
      YEBYMXPYCBDYQXPXSYBBYPXPYAXABXNYAYABHMXLXMBDERUFXOXAXABHMZXLXNXMYRDABTUGU
      HUCUCUIXNYEYEBHMXLXMBDEUJUFUKZUDUIULUDXPXQWHGZYJXILZXPYTJZXPYTCHZJZUUAXPY
      TUUCXLYTUUCMXOCBEUMSUNUUDYIXHCXPUUCCYNYTCUOQUUDXTWSYHXGXOUUCXTWSLZXLXMUUC
      UUEXNXMYTUUEUUCXMYTJXMYTAHZJUUEXMYTUUFABEUMUNUUFUUEXMUUFXSWRAYTAUOYTXSWRL
      AYTXRWPWQEBCUPUQZURUSUHUTYTCVAVBVCVDXPYHXGLUUCXPYGXFDYQXPYFXEEBYMYSXPYTYF
      XELUUBYDXDYEWNXOYTYDXDLZXLXNYTUUHXMXNYTJXNYTDHZJUUHXNYTUUIDBEUMUNUUIUUHXN
      UUIYCXCDYTDUOYTYCXCLDYTXSWRYBXBUUGYTYAWTXAEBDUPUQVEURVFUHUTVDVDYTYEWNLXPE
      BDVGUHVHVIVJVFSVKUSUTVIVJVFVLVMABCDVNXKABABCDVOVPWOXJXKWNXIBWOXHCBDCVQWOW
      RXGAWOWRXGWNWRNBWNWPWIWHIZJWRWHWIVRWNUUJWQWPBDCVSVTWAURWBWCWDWEXJAWFUTWG
      $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x v $.  $d y v $.  $d z v $.  $d w v $.
    $( A version of the Axiom of Choice with no distinct variable
       conditions. $)
    axacnd $p |- E. x A. y A. z ( A. x ( y e. z /\ z e. w ) -> E. w A. y
       ( E. w ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $=
      ( vv cv wceq wal wcel wa wex wb wi wn axacndlem5 hbnae hban dveel1
      ad2antrl dveel2 ad2antll hband hbald ax15 imp adantl impcom adantrl
      hbexd ax-12 hbbid hbimd nd5 imdistani hba1 elequ2 elequ1 anbi12d a4s
      albid syl adantlr sylan9 hbal anbi1d exbid bibi1d adantll imbi12d ex
      cbvald mpbii exp32 axacndlem2 alequcoms axacndlem3 hbae nd3 pm2.21d
      pm3.27 19.20i syl5 a5i 19.21ai 19.8a pm2.61iii ) CFZAFZGCHZWGBFZGCHZWGDFZ
      GZCHZWJWGIZWGWLIZJZAHZWQWJWLIZWLWHIZJZJZDKZWJWLGZLZBHZDKZMZCHZBHZAKZWINZW
      KNZWNNZXKXLXMXNJZJZWJEFZIZXQWLIZJZAHZXTXAJZDKZXDLZBHZDKZMZEHZBHZAKXKABEDO
      XPYIXJAXLXOACAAPXMXNACBAPCDAPQQZXPYHXIBXLXOBCABPXMXNBCBBPCDBPZQQZXPYGXHEC
      XLXOCCACPXMXNCCBCPCDCPQQZXPYAYFCYMXPXTCAYJXPXRXSCXMXRXRCHMXLXNCBERSXNXSXS
      CHMXLXMCDETUAUBZUCXPYECDXLXODCADPXMXNDCBDPCDDPQQZXPYDCBYLXPYCXDCYMXPYBCDY
      OXPXTXACYNXPWSWTCXOWSWSCHMZXLXMXNYPBDCUDUEUFXLXNWTWTCHMZXMXNXLYQDACUDUGUH
      UBUBUIXOXDXDCHMZXLXMXNYRBDCUJUEUFUKUCUIULXPXQWGGZYGXHLXPYSJYAWRYFXGXLYSYA
      WRLZXOXLYSJXLYSAHZJYTXLYSUUAACEUMUNUUAYTXLUUAXTWQAYSAUOYSXTWQLZAYSXRWOXSW
      PECBUPECDUQURZUSUTUFVAVBXOYSYFXGLZXLXOYSJXOYSBHZDHZJUUDXOYSUUFXMYSUUEXNUU
      FBCEUMXNYSDBYKDCEUMUCVCUNUUFUUDXOUUFYEXFDUUEDUOZUUFYDXEBUUEBDYSBUOVDUUFYC
      XCXDUUFYBXBDUUGUUFXTWQXAUUEUUBDYSUUBBUUCUSUSVEVFVGUTVFUFVAVHVIVJVKUTVFVLV
      MXKACABCDVNVOXKBCABCDVPVOWNXJXKWNXIBCDBVQWMXHCWNWPAHZXGWRWNUUHXGCDAVRVSWQ
      WPAWOWPVTWAWBWCWDXJAWEVAWF $.
      $( [3-Jan-2002] $)
  $}

  ${
    $d x y z w v u t $.
    $( Axiom of Extensionality, reproved from conditionless ZFC version and
       predicate calculus. $)
    zfcndext $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( cv wcel wb wal wceq wex axextnd 19.35i 19.9v sylib ) CDZADZENBDZEFZCGOP
      HZCIRQRCCABJKRCLM $.
      $( [15-Aug-2003] $)

    $( Axiom of Replacement, reproved from conditionless ZFC axioms. $)
    zfcndrep $p |- ( A. w E. y A. z ( A. y ph -> z = y ) ->
                     E. y A. z ( z e. y <-> E. w ( w e. x /\ A. y ph ) ) ) $=
      ( wal cv wceq wi wex wcel wa wb hbe1 ax-17 hba1 hban hbex hbbi hbal hbim
      elequ2 anbi1d exbidv bibi2d albidv imbi2d axrepnd 19.3 anbi1i exbii
      bibi12i albii imbi2i mpbi chvar 19.35i anbi2i a1i bibi12d cbvex sylib )
      ACFZDGZCGZHIDFZCJZEFVDEGZKZVHBGZKZVCCFZLZEJZMZDFZEJVDVEKZVKVCLZEJZMZDFZCJ
      VGVPEVGVIVHVEKZVLLZEJZMZDFZIZEJZVGVPIZEJCBWICEVGVPCVFCNVOCDVIVNCVICOZVMCE
      VKVLCVKCOVCCPQRSTZUARVEVJHZWGWIEWLWFVPVGWLWEVODWLWDVNVIWLWCVMEWLWBVKVLCBE
      UBUCUDUEUFUGUDVGVICFZWBDFZVLLZEJZMZDFZIZEJWHVCECDUHWSWGEWRWFVGWQWEDWMVIWP
      WDVICWJUIWOWCEWNWBVLWBDWBDOUIUJUKULUMUNUKUOUPUQVPWAECWKVTEDVQVSEVQEOVRENS
      TVHVEHZVOVTDWTVIVQVNVSECDUBVNVSMWTVMVREVLVCVKVCCACPUIURUKUSUTUFVAVB $.
      $( [15-Aug-2003] $)

    $( Axiom of Union, reproved from conditionless ZFC axioms. $)
    zfcndun $p |- E. y A. z ( E. w ( z e. w /\ w e. x ) -> z e. y ) $=
      ( cv wcel wa wex wi wal axunnd wceq elequ2 elequ1 anbi12d cbvexv imbi1i
      albii exbii mpbir ) CEZDEZFZUBAEZFZGZDHZUABEZFZIZCJZBHUIUHUDFZGZBHZUIIZCJ
      ZBHBCAKUKUPBUJUOCUGUNUIUFUMDBUBUHLUCUIUEULDBCMDBANOPQRST $.
      $( [15-Aug-2003] $)

    $( Axiom of Power Sets, reproved from conditionless ZFC axioms.  The
       proof uses the "Axiom of Twoness," ~ dtru . $)
    zfcndpow $p |- E. y A. z ( A. w ( w e. z -> w e. x ) -> z e. y ) $=
      ( cv wcel wi wal wex wceq wn dtru exnal mpbir hbe1 axpownd 19.23ai ax-mp
      19.9v ax-17 19.3 imbi12i albii imbi1i exbii mpbi elequ1 imbi12d cbvalv )
      DEZCEZFZUJAEZFZGZDHZUKBEZFZGZCHZBIUQUKFZUQUMFZGZBHZURGZCHZBIZVAAIZVBCHZGZ
      BHZURGZCHZBIZVGUQUKJZKZBIZVNVQVOBHKBCLVOBMNVPVNBVMBOBCAPQRVMVFBVLVECVKVDU
      RVJVCBVHVAVIVBVAASVBCVBCTUAUBUCUDUCUEUFUTVFBUSVECUPVDURUOVCDBUJUQJULVAUNV
      BDBCUGDBAUGUHUIUDUCUEN $.
      $( [15-Aug-2003] $)

    $( Axiom of Regularity, reproved from conditionless ZFC axioms.. $)
    zfcndreg $p |- ( E. y y e. x ->
                 E. y ( y e. x /\ A. z ( z e. y -> -. z e. x ) ) ) $=
      ( cv wcel wn wi wal wa wex hbe1 axregnd 19.23ai ) BDZADZEZPCDZNEQOEFGCHIZ
      BJBRBKBACLM $.
      $( [15-Aug-2003] $)

    $( Axiom of Infinity, reproved from conditionless ZFC axioms.  Since we
       have already reproved Extensionality, Replacement, and Power Sets, we
       are justified in referencing theorem ~ el in the proof. $)
    zfcndinf $p |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $=
      ( cv wcel wa wex wi wal el ax-17 hbe1 hbim hbal hban hbex axinfnd 19.35i
      syl 19.23ai ax-mp wceq elequ1 anbi1d exbidv imbi12d cbvalv anbi2i exbii
      mpbir ) AEZBEZFZCEZUMFZUODEZFZUQUMFZGZDHZIZCJZGZBHUNUNULUQFZUSGZDHZIZAJZG
      ZBHZVEDHVKADKVEVKDVJDBUNVIDUNDLZVHDAUNVGDVLVFDMNOPQVEVEBJVKVEBLVEVJBBADRS
      TUAUBVDVJBVCVIUNVBVHCAUOULUCZUPUNVAVGCABUDVMUTVFDVMURVEUSCADUDUEUFUGUHUIU
      JUK $.
      $( [15-Aug-2003] $)

    $( Axiom of Choice, reproved from conditionless ZFC axioms. $)
    zfcndac $p |- E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) ) $=
      ( cv wcel wa wex wceq wb wal wi axacnd ax-17 19.3 imbi1i 2albii exbii
      mpbi equequ2 bibi2d elequ2 anbi2d elequ1 anbi12d cbvexv bibi1i syl6bb
      albidv anbi1d exbidv equequ1 bibi12d cbvalv imbi2i mpbir ) CHZDHZIZVAAHZI
      ZJZFHZVAIZVAGHZIZJZVFVHIZVHBHZIZJZJZGKZVFEHZLZMZFNZEKZOZDNCNZBKVEVEUTVCIZ
      VCVLIZJZJZAKZUTVCLZMZCNZAKZOZDNCNZBKZVEBNZWLOZDNCNZBKWOBCDAPWRWNBWQWMCDWP
      VEWLVEBVEBQRSTUAUBWCWNBWBWMCDWAWLVEVTWKEAVQVCLZVTVGVDJZVFVCIZWEJZJZAKZVFV
      CLZMZFNWKWSVSXFFWSVSVPXEMXFWSVRXEVPEAFUCUDVPXDXEVOXCGAVHVCLZVJWTVNXBXGVIV
      DVGGADUEUFXGVKXAVMWEGAFUEGABUGUHUHUIUJUKULXFWJFCVFUTLZXDWHXEWIXHXCWGAXHWT
      VEXBWFXHVGVBVDFCDUGUMXHXAWDWEFCAUGUMUHUNFCAUOUPUQUKUIURTUAUS $.
      $( [15-Aug-2003] $)
  $}

$(
  @{
    @d x y z w @.
    zfcndpowNEW @p  |- E. x A. y ( A. x ( x e. y -> x e. z ) -> y e. x ) @=
     ( wel wi wal wex weq wa wb zfcndrep hbae 19.9rOLD equid a1bi albii exbii
       bitr
      imbi1i mpbir 19.37 mpbi ax-gen biantru bibi2i bicomi bi2 sylbi 19.20i
      19.22i syl axpowndNEW ax-mp 19.9rvOLD ax-17 19.3rOLD imbi12i )
      ABDZACDZEZAFZBADZE
      ZBFZAGURCGZUSBFZEZAFZVBEZBFZAGZABHZAFZCBDZCGZUSEZAFZCGZEVKVMUSVNAAHZBFZIZ
      CGZJZAFZCGZVRVMWDEZCGZVMWEEWGVSVLEZAFZBGZWDEZCGVSCBAKWFWKCVMWJWDVMVMBGWJV
      MBABBLMVMWIBVLWHAVSVLANZOPQRSQTVMWDCABCLUAUBWDVQCWCVPAWCUSVOJZVPWMWCVOWBU
      SVNWACVTVNVSBWLUCUDQUEUFUSVOUGUHUIUJUKABCULUMVDVJAVCVIBVAVHVBUTVGAURVEUSV
      FURCUNUSBUSBUOUPUQPSPQT @.
  @}
$)

$( End of set.mm $)

$( The work below may eventually be broken off as a separate
   module, dedekind.mm. $)

