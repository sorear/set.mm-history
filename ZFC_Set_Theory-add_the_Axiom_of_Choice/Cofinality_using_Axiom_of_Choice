$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Cofinality using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A f x y $.  $d A f y z $.
    $( A successor aleph is regular.  Theorem 11.15 of [TakeutiZaring] p. 103.
       (Contributed by Mario Carneiro, 9-Mar-2013.) $)
    alephreg $p |- ( cf ` ( aleph ` suc A ) ) = ( aleph ` suc A ) $=
      ( vf vx vy vz con0 wcel cale cfv ccf wceq csdm wbr cdom wss wral syl11anc
      wa cv syl csuc alephordilem1 cxp wf1 wrex wex alephon cff1 ax-mp ciun f1f
      wn wf ad2antrr simplr oneli ffvelrn onelon sylancr onsssuc sylan2 anassrs
      rexbidva eliun syl6bbr ancoms ralbidva dfss3 biimpa ssdomg mpsyl suceloni
      simprl wlim alephislim limsuc sylbi ccrd alephcard iscard simprbi vtoclri
      breq1 alephsucdom syl5ibr sylbid syl5 expdimp r19.21aiv fvex sucex iundom
      wb expcom 19.23adv mpi cen com alephgeom infxpen mpan xpdom1 syl6 domentr
      domtr sylsyld imp domnsym ex mt2d wo cfon onsseleq mpbii mp2an ori c0 cf0
      cfle cdm wfn alephfnon eleq2i sucelon bitr4i ndmfv sylnbir fveq2d 3eqtr4a
      fndm pm2.61i ) AFGZAUAZHIZJIZYNKZYLYOYNGZULYPYLYQAHIZYNLMZAUBYLYQYSULZYLY
      QRZYNYRNMZYTUUAYNYOYRUCZNMZUUCYRNMZUUBUUAYOYNBSZUDZCSZDSZUUFIZOZDYOUEZCYN
      PZRZBUFZUUDYNFGZUUOYMUGZCDYNBUHUIUUAUUNUUDBUUNUUAUUDUUNUUARZYNDYOUUJUAZUJ
      ZNMZUUTUUCNMZUUDUUPUURYNUUTOZUVAUUQUURYOYNUUFUMZUUMUVCUUGUVDUUMUUAYOYNUUF
      UKUNZUUGUUMUUAUOUVDUUMUVCUVDUUMUUHUUTGZCYNPUVCUVDUULUVFCYNUUHYNGUVDUUHFGZ
      UULUVFWMZYNUUHUUQUPUVGUVDUVHUVGUVDRZUULUUHUUSGZDYOUEUVFUVIUUKUVJDYOUVGUVD
      UUIYOGZUUKUVJWMZUVDUVKRZUVGUUJFGZUVLUVMUUPUUJYNGZUVNUUQYOYNUUIUUFUQZYNUUJ
      URUSUUHUUJUTVAVBVCDUUHYOUUSVDVEVFVAVGCYNUUTVHVEVIQYNUUTFVJVKUURYLUVDUVBUU
      NYLYQVMUVEYLUVDRZUUSYRNMZDYOPUVBUVQUVRDYOYLUVDUVKUVRUVMUVOYLUVRUVPYLUVOUU
      SYNGZUVRYLYMFGZUVOUVSWMZAVLUVTYNVNUWAYMVOYNUUJVPVQTUVSUVRYLUUSYNLMZESZYNL
      MZUWBEUUSYNUWCUUSYNLWCYNVRIYNKZUWDEYNPZYMVSUWEUUPUWFEYNVTWAUIZWBUUSAWDWEW
      FWGWHWIDYOYRUUSYNJWJAHWJZUUJUUIUUFWJWKWLTQYNUUTUUCXEQWNWOWPYLYQUUEYLYRYRU
      CZYRWQMZYQUUCUWINMZUUEYLWRYROZUWJAWSYRFGUWLUWJAUGYRWTXAVQYLYQYOYRNMZUWKYQ
      UWMYLYOYNLMZUWDUWNEYOYNUWCYOYNLWCUWGWBYOAWDWEYOYRYRUWHUWHXBXCUWKUWJUUEUUC
      UWIYRXDWNXFXGYNUUCYRXEQYNYRXHTXIXJYQYPYOFGZUUPYQYPXKZYNXLUUQUWOUUPRYOYNOU
      WPYNXSYOYNXMXNXOXPTYLULZXQJIXQYOYNXRUWQYNXQJYLYMHXTZGZYNXQKUWSUVTYLUWRFYM
      HFYAUWRFKYBFHYJUIYCAYDYEYMHYFYGZYHUWTYIYK $.
      $( [9-Mar-2013] $)
  $}

  ${
    $d A f w y z $.  $d A f x y z $.  $d H x $.  $d f s x y $.
    pwcfsdom.1 $e |- H = ( y e. ( cf ` ( aleph ` A ) ) |->
       { s e. On | s ~<_ ( f ` y ) } ) $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.28 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    pwcfsdom $p |- ( aleph ` A ) ~<
       ( ( aleph ` A ) ^m ( cf ` ( aleph ` A ) ) ) $=
      ( vx con0 wcel cale cfv ccf csdm wbr c0 wceq cvv wa com c2o vz vw cmap co
      csuc wrex wlim w3o onzsl biimpi cfom aleph0 fveq2i 3eqtr4i fveq2d 3eqtr4a
      cv fveq2 cdom cpw cen fvex canth2 pw2en wi oprex sdomentr ax-mp mp2an wss
      alephgeom omelon 2onn onelss mp2 sstr mpan sylbi ssdomg mapdom1 sdomdomtr
      3syl sylancr opreq2 breq2d syl5ibrcom syl5 alephreg r19.23aivw wf w3a wex
      wsmo wral alephon cfsmo cixp limelon ciun crn cuni cab wfn ffn fnrnfv syl
      unieqd dfiun2 syl6eqr ad2antrl fnfvelrn sylan sseq2 rcla4ev ex r19.23adva
      ralimdv imp adantl wb omsublim frn syl2an mpbird crab ffvelrn ccrd iscard
      simprbi mp2b breq1 elrab mpan2 mpsyl r19.21aiva eqid wn cmpt c1o syl6eq
      adantr coflim oneli ondomcard domrefg biimpri rcla4cv hartogs weq rabbidv
      eqtr3d fvmptg konigth eqbrtrrd alephlim eleq2d eliun vtoclri con2i ontri1
      alephcard domnsym sylibr alephord2i ontr2 ancoms r19.23adv syl5bi sylan9r
      exp31 sylbid cbvmptv eqtri fmptd ss2ixp ixpconst syl6sseq ssdom2g adantrr
      syl11anc expcom 3adant2 19.23aiv a1i 3jaod mpd cdm alephfnon eleq2i ndmfv
      wne 1n0 1on elisseti 0sdom mpbir id cf0 opreq12d 0ex map0e breq12d mpbiri
      fndm sylnbir pm2.61i ) BHIZBJKZUXHUXHLKZUCUDZMNZUXGBOPZBGUQZUEZPZGHUFZBQI
      BUGRZUHZUXKUXGUXRGBUIUJUXGUXLUXKUXPUXQUXLUXIUXHPZUXGUXKUXLOJKZLKZUXTUXIUX
      HSLKSUYAUXTUKUXTSLULUMULUNUXLUXHUXTLBOJURZUOUYBUPUXGUXKUXSUXHUXHUXHUCUDZM
      NZUXGUXHTUXHUCUDZMNZUYEUYCUSNZUYDUXHUXHUTZMNZUYHUYEVANZUYFUXHBJVBZVCUXHUY
      KVDUYEQIUYIUYJRUYFVETUXHUCVFUXHUYHUYEQVGVHVIUXGTUXHVJZTUXHUSNZUYGUXGSUXHV
      JZUYLBVKTSVJZUYNUYLSHITSIZUYOVLVMSTVNVOTSUXHVPVQVRUYPUYLUYMVEVMTUXHSVSVHT
      UXHUXHUYKUYKVTWBUYCQIUYFUYGRUYDVEUXHUXHUCVFUXHUYEUYCQWAVHWCUXSUXJUYCUXHMU
      XIUXHUXHUCWDWEWFZWGUXPUXSUXGUXKUXOUXSGHUXOUXNJKZLKUYRUXIUXHUXMWHUXOUXHUYR
      LBUXNJURZUOUYSUPWIUYQWGUXQUXKVEZUXGUXHHIZUXIUXHCUQZWJZVUBWMZUAUQZUBUQZVUB
      KZVJZUBUXIUFZUAUXHWNZWKZCWLUYTBWOZUAUBUXHCWPVUKUYTCVUCVUJUYTVUDUXQVUCVUJR
      ZUXKUXQVUMRUXHGUXIUXMDKZWQZMNZVUOUXJUSNZUXKUXQUXGVUMVUPBQWRZUXGVUMRZGUXIU
      XMVUBKZWSZUXHVUOMVUSVUBWTZXAZVVAUXHVUCVVCVVAPUXGVUJVUCVVCAUQZVUTPGUXIUFAX
      BZXAZVVAVUCVUBUXIXCZVVCVVFPUXIUXHVUBXDZVVGVVBVVEGAUXIVUBXEXGXFGAUXIVUTUXM
      VUBVBZXHXIXJVUSVVCUXHPZVUEVVDVJZAVVBUFZUAUXHWNZVUMVVMUXGVUCVUJVVMVUCVUIVV
      LUAUXHVUCVUHVVLUBUXIVUCVUFUXIIZRZVUHVVLVVOVUGVVBIZVUHVVLVUCVVGVVNVVPVVHUX
      IVUFVUBXKXLVVKVUHAVUGVVBVVDVUGVUEXMXNXLXOXPXQXRXSUXGUXHUGVVBUXHVJZVVJVVMX
      TVUMBYAVUCVVQVUJUXIUXHVUBYBUUAUAAUXHVVBUUBYCYDUUKVUCVVAVUOMNZUXGVUJVUCVUT
      VUNMNZGUXIWNVVRVUCVVSGUXIVUCUXMUXIIZRZVVSVUTEUQZVUTUSNZEHYEZMNZVWAVUTUXHI
      ZVUTHIZVWEUXIUXHUXMVUBYFZUXHVUTVULUUCVVDVWDMNZAVWDWNZVWGVUTVWDIZVWEVUTQIZ
      VWDYGKVWDPZVWJVVIEVUTQUUDVWMVWDHIZVWJAVWDYHYIYJVWGVUTVUTUSNZVWKVWLVWOVVIV
      UTQUUEVHVWKVWGVWORVWCVWOEVUTHVWBVUTVUTUSYKYLUUFYMVWIVWEAVUTVWDVVDVUTVWDMY
      KUUGYNWBVVTVVSVWEXTVUCVVTVUNVWDVUTMVVTVWNVUNVWDPVWLVWNVVIEVUTQUUHVHZAUXMV
      WBVVDVUBKZUSNZEHYEZVWDUXIHDAGUUIZVWRVWCEHVWTVWQVUTVWBUSVVDUXMVUBURWEUUJZF
      UULYMWEXSYDYOUXIVUOVVAGVUBDUXHLVBZVVAYPVUOYPUUMXFXJUUNXLUXQVUCVUQVUJUXJQI
      ZUXQVUCRZVUOUXJVJZVUQUXHUXIUCVFZVXDUXIUXHDWJZVUNUXHVJZGUXIWNZVXEVXDGUXIVW
      DUXHDVUCVVTVWFUXQVWDUXHIZVUCVVTVWFVWHXOUXQVWFVUTABVVDJKZWSZIZVXJUXQUXHVXL
      VUTABQUUOUUPUXQUXGVXMVXJVEVURVXMVUTVXKIZABUFUXGVXJAVUTBVXKUUQUXGVXNVXJABU
      XGVVDBIZVXNVXJVXNUXGVXORZVXJVXNVWDVXKVJZVXKUXHIZVXJVXPVXNVUTVXKMNZVXQVUEV
      XKMNZVXSUAVUTVXKVUEVUTVXKMYKVXKYGKVXKPZVXTUAVXKWNZVVDUVAVYAVXKHIZVYBUAVXK
      YHYIVHUURVXSVXKVWDIZYQZVXQVYDVXSVYDVXKVUTUSNZVXSYQVYDVYCVYFVWCVYFEVXKHVWB
      VXKVUTUSYKYLYIVXKVUTUVBXFUUSVWNVYCVXQVYEXTVWPVVDWOVWDVXKUUTVIUVCXFUXGVXOV
      XRVVDBUVDXRVWNVUAVXQVXRRVXJVEVWPVULVWDVXKUXHUVEVIYCUVFUVJUVGUVHXFUVKUVIDA
      UXIVWSYRGUXIVWDYRFAGUXIVWSVWDVXAUVLUVMUVNVXGVXHGUXIVUAVXGVVTRVUNUXHIVXHVU
      LUXIUXHUXMDYFUXHVUNVNYNYOVXIVUOGUXIUXHWQUXJGUXIVUNUXHUVOGUXIUXHVXBUYKUVPU
      VQWBVUOUXJQUVRYNUVSVXCVUPVUQRUXKVEVXFUXHVUOUXJQWAVHUVTUWAUWBUWCYJUWDUWEUW
      FUXGBJUWGZIZUXKVYGHBJHXCVYGHPUWHHJUXDVHUWIVYHYQUXHOPZUXKBJUWJVYIUXKOYSMNZ
      VYJYSOUWKUWLYSYSHUWMUWNUWOUWPVYIUXHOUXJYSMVYIUWQZVYIUXJOOUCUDYSVYIUXHOUXI
      OUCVYKVYIUXIOLKOUXHOLURUWRYTUWSOUWTUXAYTUXBUXCXFUXEUXF $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    cfpwsdom.1 $e |- B e. _V $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.29 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    cfpwsdom $p |- ( 2o ~<_ B -> ( aleph ` A ) ~< ( cf `
      ( card ` ( B ^m ( aleph ` A ) ) ) ) ) $=
      ( con0 wcel cdom wbr cale cfv cmap co ccf csdm wa wceq com cvv ax-mp c1o
      c0 vx vy vf vz c2o ccrd wi cxp cen oprex cardid ensymi cv wrex crn wn cpw
      fvex canth2 pw2en sdomentr mapdom1 sdomdomtr sylancr cfn ficard isfinite3
      mp2an sdomdom sylbi sylbir wss alephgeom omex ssdomg domtr syl2an domnsym
      wb syl expcom con2d cardidm cun iscard3 elun df-or 3bitri syl56 alephfnon
      wo mpbi wfn fvelrnb syl6ib crab cmpt eqid pwcfsdom fveq2 opreq12d breq12d
      id mpbii r19.23aivw imp ensdomtr enref mapen mapxpen entri sylancl xpdom2
      syl6 alephon biimpi infxpen domentr csuc nsuceq0 dom0 nemtbir df-2o breq2
      breq1i syl5bb biimpcd adantld mtoi xpex mapdom2 expl com12 domtri biimpri
      mt2d nsyl2 1onn syl6eq fveq2d ex cdm fndm eleq2i sylnbir wne 1n0 elisseti
      ndmfv 0sdom mpbir opreq2 map0e cardnn df-1o fveq2i 0elon cfsuc mpbiri a1d
      eqtri pm2.61i ) ADEZUEBFGZAHIZBUVEJKZUFIZLIZMGZUGZUVCUVDUVIUVCUVDNZUVHUVE
      FGZUVIUVKUVLUVFBUVEUVHUHZJKZMGZUVKUVFUVGUVHJKZMGZUVPUVNUIGZUVOUVKUVFUVGUI
      GUVGUVPMGZUVQUVGUVFBUVEJUJZUVFUKZULUVCUVDUVSUVCUVDUAUMZHIZUVGOZUADUNZUVSU
      VCUVDUVGHUOZEZUWEUVDUVEUVFMGZUVCUVGPEZUPZUWGUVDUVEUEUVEJKZMGZUWKUVFFGZUWH
      UVEUVEUQZMGZUWNUWKUIGZUWLUVEAHURZUSUVEUWQUTUWKQEUWOUWPNUWLUGUEUVEJUJUVEUW
      NUWKQVARVHUEBUVECUWQVBUVFQEZUWLUWMNUWHUGUVTUVEUWKUVFQVCRVDUVCUWIUWHUWIUVC
      UWHUPZUWIUVCNUVFUVEFGZUWSUWIUVFPFGZPUVEFGZUWTUVCUWIUVFVEEZUXAUWRUXCUWIVSU
      VTUVFQVFRUXCUVFPMGUXAUVFVGUVFPVIVJVKUVCPUVEVLZUXBAVMZPQEUXDUXBUGVNPUVEQVO
      RVJUVFPUVEVPVQUVFUVEVRVTWAWBUVGUFIUVGOZUWJUWGUGZUVFWCUXFUVGPUWFWDEUWIUWGW
      KUXGUVGWEUVGPUWFWFUWIUWGWGWHWLWIHDWMZUWGUWEVSWJUADUVGHWNRWOUWDUVSUADUWDUW
      CUWCUWCLIZJKZMGUVSUBUWBUCUBUXIUDUMUBUMUCUMIFGUDDWPWQZUDUXKWRWSUWDUWCUVGUX
      JUVPMUWDXCZUWDUWCUVGUXIUVHJUXLUWCUVGLWTXAXBXDXEXNXFUVFUVGUVPXGVDUVPUVFUVH
      JKZUVNUVGUVFUIGUVHUVHUIGUVPUXMUIGUWAUVHUVGLURZXHUVGUVFUVHUVHUVTUXNXIVHBUV
      EUVHCUWQUXNXJXKUVNQEUVQUVRNUVOUGBUVMJUJUVFUVPUVNQVARXLUVLUVKUVOUPZUVLUVCU
      VDUXOUVLUVCNZUVDNUVNUVFFGZUXOUXPUVMUVEFGZUVMTOZBTOZNZUPUXQUVDUVLUVMUVEUVE
      UHZFGUYBUVEUIGZUXRUVCUVHUVEUVEUWQUWQXMUVCUVEDEUXDUYCAXOUVCUXDUXEXPUVEXQVD
      UVMUYBUVEXRVQUVDUYASXSZTFGZUYEUYDTSXTUYDYAYBUVDUXTUYEUXSUXTUVDUYEUVDUYDBF
      GUXTUYEUEUYDBFYCYEBTUYDFYDYFYGYHYIUVMUVEBUVEUVHUWQUXNYJUWQCYKVQUVNUVFVRVT
      YLYMYPUVLUVIUPZUVHQEUVEQEUVLUYFVSUXNUWQUVHUVEQQYNVHYOYQUUAUVCUPUVETOZUVJU
      VCAHUUBZEUYGUYHDAUXHUYHDOWJDHUUCRUUDAHUUIUUEUYGUVIUVDUYGUVITSMGZUYISTUUFU
      UGSSPYRUUHUUJUUKUYGUVETUVHSMUYGXCUYGUVHSLIZSUYGUVGSLUYGUVGSUFIZSUYGUVFSUF
      UYGUVFBTJKSUVETBJUULBCUUMYSYTSPEUYKSOYRSUUNRYSYTUYJTXSZLIZSSUYLLUUOUUPTDE
      UYMSOUUQTUURRUVAYSXBUUSUUTVTUVB $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    $( From ~ canth2 , we know that ` ( aleph `` 0 ) < ( 2 ^ om ) ` , but we
       cannot prove that ` ( 2 ^ om ) = ( aleph `` 1 ) ` (this is the Continuum
       Hypothesis), nor can we prove that it is less than any bound whatsoever
       (i.e. the statement ` ( aleph `` A ) < ( 2 ^ om ) ` is consistent for
       any ordinal ` A ` ).  However, we can prove that ` ( 2 ^ om ) ` is not
       equal to ` ( aleph `` om ) ` , nor ` ( aleph `` ( aleph `` om ) ) ` , on
       cofinality grounds, because by Konig's Theorem ~ konigth (in the form of
       ~ cfpwsdom ), ` ( 2 ^ om ) ` has uncountable cofinality, which
       eliminates limit alephs like ` ( aleph `` om ) ` .  (The first limit
       aleph that is _not_ eliminated is ` ( aleph `` ( aleph `` 1 ) ) ` ,
       which has cofinality ` ( aleph `` 1 ) ` .)  (Contributed by Mario
       Carneiro, 21-Mar-2013.) $)
    alephom $p |- ( card ` ( 2o ^m om ) ) =/= ( aleph ` om ) $=
      ( com csdm wbr wn c2o cmap co ccrd cfv cale wne sdomirr wceq ccf cvv wcel
      c0 cdom aleph0 ax-mp 2onn elisseti domrefg cfpwsdom opreq2i fveq2i eqeq1i
      mp2b a1i biimpri fveq2d wlim limom alephsing cfom syl6eq breq12d necon3bi
      eqtri mpbii ) AABCZDEAFGZHIZAJIZKALVAVCVDVCVDMZQJIZEVFFGZHIZNIZBCZVAEOPEE
      RCVJEAUAUBZEOUCQEVKUDUHVEVFAVIABVFAMVESUIVEVIVDNIZAVEVHVDNVHVDMVEVHVCVDVG
      VBHVFAEFSUEUFUGUJUKVLANIZAAULVLVMMUMAUNTUOUSUPUQUTURT $.
      $( [21-Mar-2013] $)
  $}


  $c InaccW $. $( Weakly inaccessible $)
  $c Inacc $. $( Strongly inaccessible $)

  $( The class of weak inaccessibles. $)
  cwina $a class InaccW $.

  $( The class of strong inaccessibles. $)
  cina $a class Inacc $.

  ${
    $d A x y $.
    $( An ordinal is weakly inaccessible iff it is a regular limit cardinal.
       Note that our definition allows ` om ` as a weakly inacessible
       cardinal. $)
    df-wina $a |- InaccW = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x E. z e. x y ~< z ) } $.

    $( An ordinal is strongly inaccessible iff it is a regular strong limit
       cardinal, which is to say that it dominates the powersets of every
       smaller ordinal. $)
    df-ina $a |- Inacc = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x ~P y ~< x ) } $.
  $}

  ${
    $d A x y z $.
    $( Conditions of weak inaccessibility. $)
    elwina $p |- ( A e. InaccW <-> ( A =/= (/) /\ ( cf ` A ) = A /\
        A. x e. A E. y e. A x ~< y ) ) $=
      ( vz cwina wcel cvv c0 wne ccf cfv wceq cv csdm wbr wrex wral w3a elisset
      fvex eleq1 mpbii 3ad2ant2 neeq1 fveq2 eqeq12 mpancom raleqbi1dv 3anbi123d
      wb rexeq df-wina elab2g pm5.21nii ) CEFCGFZCHIZCJKZCLZAMBMNOZBCPZACQZRZCE
      SURUPUOVAURUQGFUOCJTUQCGUAUBUCDMZHIZVCJKZVCLZUSBVCPZAVCQZRVBDCEGVCCLZVDUP
      VFURVHVAVCCHUDVEUQLVIVFURUJVCCJUEVEUQVCCUFUGVGUTAVCCUSBVCCUKUHUIDABULUMUN
      $.
      $( [22-Jun-2013] $)

    $( Conditions of strong inaccessibility. $)
    elina $p |- ( A e. Inacc <-> ( A =/= (/) /\ ( cf ` A ) = A /\
      A. x e. A ~P x ~< A ) ) $=
      ( vy cina wcel cvv c0 wne ccf cfv wceq cpw csdm wbr wral w3a elisset fvex
      cv eleq1 mpbii 3ad2ant2 neeq1 wb fveq2 mpancom breq2 raleqbi1dv 3anbi123d
      eqeq12 df-ina elab2g pm5.21nii ) BDEBFEZBGHZBIJZBKZASLZBMNZABOZPZBDQUQUOU
      NUTUQUPFEUNBIRUPBFTUAUBCSZGHZVBIJZVBKZURVBMNZAVBOZPVACBDFVBBKZVCUOVEUQVGU
      TVBBGUCVDUPKVHVEUQUDVBBIUEVDUPVBBUJUFVFUSAVBBVBBURMUGUHUICAUKULUM $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is an ordinal. $)
    winaon $p |- ( A e. InaccW -> A e. On ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq csdm wbr wrex wral w3a con0 elwina
      cv cfon eleq1 mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBRCRKLCAMBANZOAPEZBCAQ
      UFUDUHUGUFUEPEUHASUEAPTUAUBUC $.
      $( [29-May-2013] $)

    $( Lemma for ~ inawina . $)
    inawinalem $p |- ( A e. On ->
        ( A. x e. A ~P x ~< A -> A. x e. A E. y e. A x ~< y ) ) $=
      ( con0 wcel cv cpw csdm wbr wrex wa cen cdom sdomdom breq2 rcla4ev wi mpd
      visset cvv sylan2 syl ensdomtr ad2ant2l sdomel ad2ant2r canth2 pwex ensym
      ondomen sdomentr ax-mp sylancr ad2antlr jca expcom reximdv2 ex ralimdv )
      CDEZAFZGZCHIZVABFZHIZBCJZACUTVCVFUTVCKZVDVBLIZBDJZVFVGVBVDMIZBDJZVIVCUTVB
      CMIZVKVBCNVJVLBCDVDCVBMOPUABBVBUJUBVGVHVEBDCVDDEZVHKZVGVDCEZVEKVNVGKZVOVE
      VPVDCHIZVOVHVCVQVMUTVDVBCUCUDVMUTVQVOQVHVCVDCUEUFRVHVEVMVGVHVAVBHIZVBVDLI
      ZVEVAASZUGVDVBVAVTUHUIVDTEVRVSKVEQBSVAVBVDTUKULUMUNUOUPUQRURUS $.
      $( [8-Jun-2013] $)

    $( Every strongly inaccessible cardinal is weakly inaccessible. $)
    inawina $p |- ( A e. Inacc -> A e. InaccW ) $=
      ( vx vy c0 wne ccf cfv wceq cv cpw csdm wbr wral w3a wrex cina wcel cwina
      con0 idd cfon eleq1 mpbii inawinalem 3anim123d mpcom elina elwina 3imtr4i
      3ad2ant2 ) ADEZAFGZAHZBIZJAKLBAMZNZUKUMUNCIKLCAOBAMZNZAPQARQASQZUPURUMUKU
      SUOUMULSQUSAUAULASUBUCUJUSUKUKUMUMUOUQUSUKTUSUMTBCAUDUEUFBAUGBCAUHUI $.
      $( [29-May-2013] $)
  $}

  ${
    $( ` om ` is a strongly inaccessible cardinal.  (Many definitions of
       "inaccessible" explicitly disallow ` om ` as an inaccessible cardinal,
       but this choice allows us to reuse our results for inaccessibles for
       ` om ` .) $)
    omina $p |- om e. Inacc $=
      ( vx com cina wcel c0 wne ccf cfv wceq cv cpw csdm wral elina peano1 ne0i
      wbr ax-mp cfn sylib cfom ssid ssnnfi mpan2 pwfi isfinite3 rgen mpbir3an
      wss ) BCDBEFZBGHBIAJZKZBLQZABMABNEBDUJOBEPRUAUMABUKBDZULSDZUMUNUKSDZUOUNU
      KUKUIUPUKUBUKUKUCUDUKUETULUFTUGUH $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is a cardinal. $)
    winacard $p |- ( A e. InaccW -> ( card ` A ) = A ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq csdm wbr wrex wral w3a ccrd elwina
      cv cardcf fveq2 id eqeq12d mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBRCRKLCAM
      BANZOAPIZAJZBCAQUHUFUKUIUHUGPIZUGJUKASUHULUJUGAUGAPTUHUAUBUCUDUE $.
      $( [29-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( A weakly inaccessible cardinal is infinite. $)
    winainflem $p |- ( ( A =/= (/) /\ A e. On /\
        A. x e. A E. y e. A x ~< y ) -> om C_ A ) $=
      ( vz vw c0 con0 wcel cv csdm wbr wrex w3a com wss wn wceq wa wi syl simp1
      wne wral csuc wo nn0suc necon2bi visset sucid eleq2 mpbiri adantl rexbidv
      weq breq1 breq2 cbvrexv syl6bb rcla4v cdom biimpa 3ad2antl2 nnon suceloni
      wb eleq1 biimparc sylan 3adant3 onelon simpl1 onsssuc syl11anc mpbird cvv
      ssdomg ax-mp domnsym 3syl nrexdv pm2.65d simp3 nsyl r19.23aiva jaoi con2i
      3expia word ordom eloni 3ad2ant2 ordtri1 sylancr ) CFUBZCGHZAIZBIZJKZBCLZ
      ACUCZMZNCOZCNHZPZXCXAXCCFQZCDIZUDZQZDNLZUEXAPZDCUFXEXJXIXACFWNWOWTUAUGXHX
      JDNXFNHZXHRZWTXAXLWTXFEIZJKZECLZXLXFCHZWTXOSXHXPXKXHXPXFXGHXFDUHUICXGXFUJ
      UKULWSXOAXFCADUNZWSXFWQJKZBCLXOXQWRXRBCWPXFWQJUOUMXRXNBECWQXMXFJUPUQURUST
      XKXHWTXOPXKXHWTMZXNECXSXMCHZRZXMXFOZXMXFUTKZXNPYAYBXMXGHZXHXKXTYDWTXHXTYD
      CXGXMUJVAVBYAXMGHZXFGHZYBYDVEXSWOXTYEXKXHWOWTXKXGGHZXHWOXKYFYGXFVCZXFVDTX
      HWOYGCXGGVFVGVHVICXMVJVHYAXKYFXKXHWTXTVKYHTXMXFVLVMVNXMVOHYBYCSEUHXMXFVOV
      PVQXMXFVRVSVTWGWAWNWOWTWBWCWDWETWFXANWHCWHZXBXDVEWIWOWNYIWTCWJWKNCWLWMVN
      $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is infinite. $)
    winainf $p |- ( A e. InaccW -> om C_ A ) $=
      ( vx vy cwina wcel wne ccf cfv wceq csdm wbr wrex wral w3a com wss elwina
      c0 cv con0 cfon eleq1 mpbii winainflem syl3an2 sylbi ) ADEARFZAGHZAIZBSCS
      JKCALBAMZNOAPZBCAQUIUGATEZUJUKUIUHTEULAUAUHATUBUCBCAUDUEUF $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is a limit ordinal. $)
    winalim $p |- ( A e. InaccW -> Lim A ) $=
      ( cwina wcel com wss wlim winainf ccrd cfv wceq wb winacard cardlim sseq2
      limeq bibi12d mpbii syl mpbid ) ABCZDAEZAFZAGTAHIZAJZUAUBKZALUDDUCEZUCFZK
      UEAMUDUFUAUGUBUCADNUCAOPQRS $.
      $( [29-May-2013] $)

    $( A nontrivial weakly inaccessible cardinal is a limit aleph. $)
    winalim2 $p |- ( ( A e. InaccW /\ A =/= om ) ->
      E. x ( ( aleph ` x ) = A /\ Lim x ) ) $=
      ( vy vw vz wcel com wne wa cv cale cfv wceq con0 wrex wex c0 wn csdm wbr
      cwina wlim ccrd winacard wss winainf cardalephex syl adantr df-rex simprr
      wb mpbid eqcomd csuc cvv w3o simprl onzsl sylib simplr aleph0 syl6eq eqtr
      fveq2 sylan2 ex necon3ad sylc pm2.21d wral suceloni sucid alephord2i ee10
      visset ad2antrl simplrr ad2antll eqtrd ccf elwina simp3bi ad3antrrr breq1
      eleqtrrd rexbidv rcla4va syl11anc expr wi iscard simprbi 3syl alephnbtwn2
      ra4 breq2d sylibd pm3.21 mtoi syl6 imp nrexdv pm2.65d simpr a1i 3jaod mpd
      jca eximdv syl5bi ) BUAFZBGHZIZBAJZKLZMZANOZXPBMZXOUBZIZAPZXLXRXMXLBUCLBM
      ZXRBUDZXLGBUEYCXRULBUFABUGUHUMUIXRXONFZXQIZAPXNYBXQANUJXNYFYAAXNYFYAXNYFI
      ZXSXTYGBXPXNYEXQUKZUNYGXOQMZXOCJZUOZMZCNOZXOUPFZXTIZUQZXTYGYEYPXNYEXQURCX
      OUSUTYGYIXTYMYOYGYIXTYGXQXMYIRYHXLXMYFVAXQYIBGXQYIBGMZYIXQXPGMYQYIXPQKLGX
      OQKVEVBVCBXPGVDVFVGVHVIVJYGYMXTYGYLCNYGYJNFZIYLYJKLZDJZSTZDBOZYGYRYLUUBYG
      YRYLIZIZYSBFEJZYTSTZDBOZEBVKZUUBUUDYSYKKLZBYRYSUUIFZYGYLYRYKNFYJYKFUUJYJV
      LYJCVPVMYJYKVNVOVQUUDBXPUUIXNYEXQUUCVRYLXPUUIMYGYRXOYKKVEVSVTZWFXLUUHXMYF
      UUCXLBQHBWALBMUUHEDBWBWCWDUUGUUBEYSBUUEYSMUUFUUADBUUEYSYTSWEWGWHWIWJYGYRY
      LUUBRUUDUUADBUUDYTBFZUUARZUUDUULYTUUISTZUUMUUDUULYTBSTZUUNXLUULUUOWKZXMYF
      UUCXLYCUUODBVKZUUPYDYCBNFUUQDBWLWMUUODBWPWNWDUUDBUUIYTSUUKWQWRUUNUUAUUAUU
      NIYJYTWOUUNUUAWSWTXAXBXCWJXDXCVJYOXTWKYGYNXTXEXFXGXHXIVGXJXKXH $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x $.  $d A y z $.
    $( A nontrivial weakly inaccessible cardinal is a fixed point of the aleph
       function. $)
    winafp $p |- ( ( A e. InaccW /\ A =/= om ) -> ( aleph ` A ) = A ) $=
      ( vx vy vz cwina wcel com wne cale cfv wceq wlim wex wss cvv ad2antll ccf
      wa cv fveq2d winalim2 con0 visset limelon mpan alephle syl simprl sseqtrd
      alephsing c0 csdm wrex wral elwina simp2bi ad2antrr 3eqtr3d cfle eqsstr3d
      wbr a1i eqssd eqtr3d ex 19.23adv mpd ) AEFZAGHZRZBSZIJZAKZVKLZRZBMAIJZAKZ
      BAUAVJVOVQBVJVOVQVJVORZVLVPAVRVKAIVRVKAVRVKVLAVNVKVLNZVJVMVNVKUBFZVSVKOFV
      NVTBUCVKOUDUEVKUFUGPVJVMVNUHZUIVRAVKQJZVKVRVLQJZAQJZWBAVRVLAQWATVNWCWBKVJ
      VMVKUJPVHWDAKZVIVOVHAUKHWECSDSULVADAUMCAUNCDAUOUPUQURWBVKNVRVKUSVBUTVCTWA
      VDVEVFVG $.
      $( [29-May-2013] $)
  $}

  ${
    $d x y $.
    $( The beth function is strictly monotone.  This proof needs AC to ensure
       that each level of the hierarchy is well-orderable, and conversely this
       implies that every set is well-orderable, so this is an AC equivalent.
       This function is not strictly the beth function, but rather beth_A is
       the same as ` ( card `` ( R1 `` ( om +o A ) ) ) ` , since conventionally
       we start counting at the first infinite level, and ignore the finite
       levels. $)
    smobeth $p |- Smo ( card o. R1 ) $=
      ( vx vy ccrd cr1 ccom con0 cvv wf cardf wfn mp2an cv wcel wa wb fvex wceq
      cfv fvco3 mp3an12 r1fnon dffn2 mpbi fco ordon wel csdm wbr r1sdom adantll
      cardsdom sylibr wfun ffun ax-mp eleq12 syl2an adantr mpbird ex fdmi issmo
      ) ABCDEZFGFCHZFGDHZFFVCHIDFJVEUAFDUBUCZFGFCDUDKZUEALZFMZBLZFMZNZABUFZVHVC
      RZVJVCRZMZVLVMNZVPVHDRZCRZVJDRZCRZMZVQVRVTUGUHZWBVKVMWCVIVJVHUIUJVRGMVTGM
      WBWCOVHDPVJDPVRVTGGUKKULVLVPWBOZVMVIVNVSQZVOWAQZWDVKCUMZVEVIWEVDWGIGFCUNU
      OZVFFGVHCDSTWGVEVKWFWHVFFGVJCDSTVNVSVOWAUPUQURUSUTFFVCVGVAVB $.
      $( [6-Jun-2013] $)
  $}


