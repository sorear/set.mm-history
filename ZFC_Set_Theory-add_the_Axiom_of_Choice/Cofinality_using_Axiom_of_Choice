$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Cofinality using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A f x y $.  $d A f y z $.
    $( A successor aleph is regular.  Theorem 11.15 of [TakeutiZaring] p. 103.
       (Contributed by Mario Carneiro, 9-Mar-2013.) $)
    alephreg $p |- ( cf ` ( aleph ` suc A ) ) = ( aleph ` suc A ) $=
      ( vf vx vy vz con0 wcel csuc cale cfv ccf wceq wn csdm wbr alephordilem1
      wa cdom cxp cv wf1 wss wrex wral wex alephon cff1 ax-mp ciun wf f1f
      ad2antrr simplr wb oneli ffvelrn onelon sylancr onsssuc sylan2 anassrs
      rexbidva eliun syl6bbr ancoms ralbidva dfss3 biimpa syl11anc ssdomg
      mpsyl simprl suceloni wlim alephislim limsuc sylbi syl breq1 ccrd
      alephcard iscard simprbi vtoclri alephsucdom syl5ibr sylbid syl5 expdimp
      r19.21aiv fvex sucex iundom domtr expcom 19.23adv mpi cen com alephgeom
      infxpen mpan xpdom1 syl6 domentr sylsyld imp domnsym ex mt2d wo cfon
      cfle onsseleq mpbii mp2an ori c0 cf0 cdm wfn alephfnon fndm eleq2i
      sucelon bitr4i notbii ndmfv sylbir fveq2d 3eqtr4a pm2.61i ) AFGZAHZIJZKJZ
      YOLZYMYPYOGZMYQYMYRAIJZYONOZAPYMYRYTMZYMYRQZYOYSROZUUAUUBYOYPYSSZROZUUDYS
      ROZUUCUUBYPYOBTZUAZCTZDTZUUGJZUBZDYPUCZCYOUDZQZBUEZUUEYOFGZUUPYNUFZCDYOBU
      GUHUUBUUOUUEBUUOUUBUUEUUOUUBQZYODYPUUKHZUIZROZUVAUUDROZUUEUUQUUSYOUVAUBZU
      VBUURUUSYPYOUUGUJZUUNUVDUUHUVEUUNUUBYPYOUUGUKULZUUHUUNUUBUMUVEUUNUVDUVEUU
      NUUIUVAGZCYOUDUVDUVEUUMUVGCYOUUIYOGUVEUUIFGZUUMUVGUNZYOUUIUURUOUVHUVEUVIU
      VHUVEQZUUMUUIUUTGZDYPUCUVGUVJUULUVKDYPUVHUVEUUJYPGZUULUVKUNZUVEUVLQZUVHUU
      KFGZUVMUVNUUQUUKYOGZUVOUURYPYOUUJUUGUPZYOUUKUQURUUIUUKUSUTVAVBDUUIYPUUTVC
      VDVEUTVFCYOUVAVGVDVHVIYOUVAFVJVKUUSYMUVEUVCUUOYMYRVLUVFYMUVEQZUUTYSROZDYP
      UDUVCUVRUVSDYPYMUVEUVLUVSUVNUVPYMUVSUVQYMUVPUUTYOGZUVSYMYNFGZUVPUVTUNZAVM
      UWAYOVNUWBYNVOYOUUKVPVQVRUVTUVSYMUUTYONOZETZYONOZUWCEUUTYOUWDUUTYONVSYOVT
      JYOLZUWEEYOUDZYNWAUWFUUQUWGEYOWBWCUHZWDUUTAWEWFWGWHWIWJDYPYSUUTYOKWKAIWKZ
      UUKUUJUUGWKWLWMVRVIYOUVAUUDWNVIWOWPWQYMYRUUFYMYSYSSZYSWROZYRUUDUWJROZUUFY
      MWSYSUBZUWKAWTYSFGUWMUWKAUFYSXAXBVQYMYRYPYSROZUWLYRUWNYMYPYONOZUWEUWOEYPY
      OUWDYPYONVSUWHWDYPAWEWFYPYSYSUWIUWIXCXDUWLUWKUUFUUDUWJYSXEWOXFXGYOUUDYSWN
      VIYOYSXHVRXIXJYRYQYPFGZUUQYRYQXKZYOXLUURUWPUUQQYPYOUBUWQYOXMYPYOXNXOXPXQV
      RYMMZXRKJXRYPYOXSUWRYOXRKUWRYNIXTZGZMYOXRLUWTYMUWTUWAYMUWSFYNIFYAUWSFLYBF
      IYCUHYDAYEYFYGYNIYHYIZYJUXAYKYL $.
      $( [9-Mar-2013] $)
  $}

  ${
    $d A f w y z $.  $d A f x y z $.  $d H x $.  $d f s x y $.
    pwcfsdom.1 $e |- H = ( y e. ( cf ` ( aleph ` A ) ) |->
       { s e. On | s ~<_ ( f ` y ) } ) $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.28 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    pwcfsdom $p |- ( aleph ` A ) ~<
       ( ( aleph ` A ) ^m ( cf ` ( aleph ` A ) ) ) $=
      ( vx vz vw con0 wcel cale cfv ccf cmap co csdm wbr c0 wceq cv csuc wrex
      cvv wlim wa w3o onzsl biimpi com cfom aleph0 fveq2i 3eqtr4i fveq2 fveq2d
      3eqtr4a c2o cdom cpw cen fvex canth2 pw2en wi oprex sdomentr ax-mp mp2an
      wss alephgeom omelon 2onn onelss mp2 sstr mpan sylbi ssdomg mapdom1 3syl
      sdomdomtr sylancr opreq2 breq2d syl5ibrcom syl5 alephreg r19.23aivw wf
      wsmo wral w3a wex alephon cfsmo cixp limelon ciun crn cuni cab wfn ffn
      fnrnfv unieqd syl dfiun2 syl6eqr ad2antrl fnfvelrn sylan sseq2 rcla4ev
      ex r19.23adva ralimdv imp adantl wb omsublim frn adantr coflim syl2an
      mpbird eqtr3d crab ffvelrn oneli ccrd ondomcard iscard simprbi mp2b
      domrefg breq1 elrab biimpri mpan2 rcla4cv mpsyl hartogs weq rabbidv
      fvmptg r19.21aiva eqid konigth eqbrtrrd alephlim eleq2d eliun alephcard
      vtoclri wn domnsym con2i ontri1 sylibr alephord2i ontr2 ancoms exp31
      r19.23adv syl5bi sylbid sylan9r cmpt cbvmptv eqtri fmptd ss2ixp ixpconst
      syl6sseq ssdom2g adantrr syl11anc expcom 3adant2 19.23aiv a1i 3jaod mpd
      cdm alephfnon fndm eleq2i notbii ndmfv c1o wne 1n0 1on elisseti 0sdom
      mpbir id cf0 syl6eq opreq12d 0ex map0e breq12d mpbiri sylbir pm2.61i ) BJ
      KZBLMZUXIUXINMZOPZQRZUXHBSTZBGUAZUBZTZGJUCZBUDKBUEUFZUGZUXLUXHUXSGBUHUIUX
      HUXMUXLUXQUXRUXMUXJUXITZUXHUXLUXMSLMZNMZUYAUXJUXIUJNMUJUYBUYAUKUYAUJNULUM
      ULUNUXMUXIUYANBSLUOZUPUYCUQUXHUXLUXTUXIUXIUXIOPZQRZUXHUXIURUXIOPZQRZUYFUY
      DUSRZUYEUXIUXIUTZQRZUYIUYFVARZUYGUXIBLVBZVCUXIUYLVDUYFUDKUYJUYKUFUYGVEURU
      XIOVFUXIUYIUYFUDVGVHVIUXHURUXIVJZURUXIUSRZUYHUXHUJUXIVJZUYMBVKURUJVJZUYOU
      YMUJJKURUJKZUYPVLVMUJURVNVOURUJUXIVPVQVRUYQUYMUYNVEVMURUXIUJVSVHURUXIUXIU
      YLUYLVTWAUYDUDKUYGUYHUFUYEVEUXIUXIOVFUXIUYFUYDUDWBVHWCUXTUXKUYDUXIQUXJUXI
      UXIOWDWEWFZWGUXQUXTUXHUXLUXPUXTGJUXPUXOLMZNMUYSUXJUXIUXNWHUXPUXIUYSNBUXOL
      UOZUPUYTUQWIUYRWGUXRUXLVEZUXHUXIJKZUXJUXICUAZWJZVUCWKZHUAZIUAZVUCMZVJZIUX
      JUCZHUXIWLZWMZCWNVUABWOZHIUXICWPVULVUACVUDVUKVUAVUEUXRVUDVUKUFZUXLUXRVUNU
      FUXIGUXJUXNDMZWQZQRZVUPUXKUSRZUXLUXRUXHVUNVUQBUDWRZUXHVUNUFZGUXJUXNVUCMZW
      SZUXIVUPQVUTVUCWTZXAZVVBUXIVUDVVDVVBTUXHVUKVUDVVDAUAZVVATGUXJUCAXBZXAZVVB
      VUDVUCUXJXCZVVDVVGTUXJUXIVUCXDZVVHVVCVVFGAUXJVUCXEXFXGGAUXJVVAUXNVUCVBZXH
      XIXJVUTVVDUXITZVUFVVEVJZAVVCUCZHUXIWLZVUNVVNUXHVUDVUKVVNVUDVUJVVMHUXIVUDV
      UIVVMIUXJVUDVUGUXJKZUFZVUIVVMVVPVUHVVCKZVUIVVMVUDVVHVVOVVQVVIUXJVUGVUCXKX
      LVVLVUIAVUHVVCVVEVUHVUFXMXNXLXOXPXQXRXSUXHUXIUEVVCUXIVJZVVKVVNXTVUNBYAVUD
      VVRVUKUXJUXIVUCYBYCHAUXIVVCYDYEYFYGVUDVVBVUPQRZUXHVUKVUDVVAVUOQRZGUXJWLVV
      SVUDVVTGUXJVUDUXNUXJKZUFZVVTVVAEUAZVVAUSRZEJYHZQRZVWBVVAUXIKZVVAJKZVWFUXJ
      UXIUXNVUCYIZUXIVVAVUMYJVVEVWEQRZAVWEWLZVWHVVAVWEKZVWFVVAUDKZVWEYKMVWETZVW
      KVVJEVVAUDYLVWNVWEJKZVWKAVWEYMYNYOVWHVVAVVAUSRZVWLVWMVWPVVJVVAUDYPVHVWLVW
      HVWPUFVWDVWPEVVAJVWCVVAVVAUSYQYRYSYTVWJVWFAVVAVWEVVEVVAVWEQYQUUAUUBWAVWAV
      VTVWFXTVUDVWAVUOVWEVVAQVWAVWOVUOVWETVWMVWOVVJEVVAUDUUCVHZAUXNVWCVVEVUCMZU
      SRZEJYHZVWEUXJJDAGUUDZVWSVWDEJVXAVWRVVAVWCUSVVEUXNVUCUOWEUUEZFUUFYTWEXSYF
      UUGUXJVUPVVBGVUCDUXINVBZVVBUUHVUPUUHUUIXGXJUUJXLUXRVUDVURVUKUXKUDKZUXRVUD
      UFZVUPUXKVJZVURUXIUXJOVFZVXEUXJUXIDWJZVUOUXIVJZGUXJWLZVXFVXEGUXJVWEUXIDVU
      DVWAVWGUXRVWEUXIKZVUDVWAVWGVWIXOUXRVWGVVAABVVELMZWSZKZVXKUXRUXIVXMVVAABUD
      UUKUULUXRUXHVXNVXKVEVUSVXNVVAVXLKZABUCUXHVXKAVVABVXLUUMUXHVXOVXKABUXHVVEB
      KZVXOVXKVXOUXHVXPUFZVXKVXOVWEVXLVJZVXLUXIKZVXKVXQVXOVVAVXLQRZVXRVUFVXLQRZ
      VXTHVVAVXLVUFVVAVXLQYQVXLYKMVXLTZVYAHVXLWLZVVEUUNVYBVXLJKZVYCHVXLYMYNVHUU
      OVXTVXLVWEKZUUPZVXRVYEVXTVYEVXLVVAUSRZVXTUUPVYEVYDVYGVWDVYGEVXLJVWCVXLVVA
      USYQYRYNVXLVVAUUQXGUURVWOVYDVXRVYFXTVWQVVEWOVWEVXLUUSVIUUTXGUXHVXPVXSVVEB
      UVAXRVWOVUBVXRVXSUFVXKVEVWQVUMVWEVXLUXIUVBVIYEUVCUVDUVEUVFXGUVGUVHDAUXJVW
      TUVIGUXJVWEUVIFAGUXJVWTVWEVXBUVJUVKUVLVXHVXIGUXJVUBVXHVWAUFVUOUXIKVXIVUMU
      XJUXIUXNDYIUXIVUOVNUUBUUGVXJVUPGUXJUXIWQUXKGUXJVUOUXIUVMGUXJUXIVXCUYLUVNU
      VOWAVUPUXKUDUVPUUBUVQVXDVUQVURUFUXLVEVXGUXIVUPUXKUDWBVHUVRUVSUVTUWAYOUWBU
      WCUWDUXHUUPBLUWEZKZUUPZUXLVYIUXHVYHJBLJXCVYHJTUWFJLUWGVHUWHUWIVYJUXISTZUX
      LBLUWJVYKUXLSUWKQRZVYLUWKSUWLUWMUWKUWKJUWNUWOUWPUWQVYKUXISUXKUWKQVYKUWRZV
      YKUXKSSOPUWKVYKUXISUXJSOVYMVYKUXJSNMSUXISNUOUWSUWTUXASUXBUXCUWTUXDUXEXGUX
      FUXG $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    cfpwsdom.1 $e |- B e. _V $.
    $( A corollary of Konig's Theorem ~ konigth .  Theorem 11.29 of
       [TakeutiZaring] p. 108.  (Contributed by Mario Carneiro,
       20-Mar-2013.) $)
    cfpwsdom $p |- ( 2o ~<_ B -> ( aleph ` A ) ~< ( cf `
      ( card ` ( B ^m ( aleph ` A ) ) ) ) ) $=
      ( vx vy vf vz con0 wcel c2o cdom wbr cale cfv cmap co ccrd ccf csdm wi
      wa cxp cen oprex cardid ensymi cv wceq wrex crn com wn cpw fvex canth2
      pw2en cvv sdomentr ax-mp mp2an mapdom1 sdomdomtr sylancr cfn wb ficard
      isfinite3 sdomdom sylbi sylbir wss alephgeom omex ssdomg domtr syl2an
      domnsym syl expcom con2d syl5 cardidm cun wo iscard3 elun df-or 3bitri
      mpbi syl6 wfn alephfnon fvelrnb syl6ib crab cmpt eqid pwcfsdom id fveq2
      opreq12d breq12d mpbii r19.23aivw imp ensdomtr enref mapen mapxpen entri
      sylancl c0 xpdom2 alephon biimpi infxpen domentr c1o csuc nsuceq0 dom0
      nemtbir df-2o breq1i breq2 syl5bb biimpcd adantld mtoi xpex mapdom2 expl
      com12 mt2d domtri biimpri nsyl2 ex cdm fndm eleq2i notbii ndmfv wne 1n0
      1onn elisseti 0sdom mpbir opreq2 map0e syl6eq fveq2d cardnn df-1o fveq2i
      0elon cfsuc eqtri mpbiri a1d pm2.61i ) AHIZJBKLZAMNZBUVEOPZQNZRNZSLZTZUVC
      UVDUVIUVCUVDUAZUVHUVEKLZUVIUVKUVLUVFBUVEUVHUBZOPZSLZUVKUVFUVGUVHOPZSLZUVP
      UVNUCLZUVOUVKUVFUVGUCLUVGUVPSLZUVQUVGUVFBUVEOUDZUVFUEZUFUVCUVDUVSUVCUVDDU
      GZMNZUVGUHZDHUIZUVSUVCUVDUVGMUJZIZUWEUVCUVDUVGUKIZULZUWGUVDUVEUVFSLZUVCUW
      IUVDUVEJUVEOPZSLZUWKUVFKLZUWJUVEUVEUMZSLZUWNUWKUCLZUWLUVEAMUNZUOUVEUWQUPU
      WKUQIUWOUWPUAUWLTJUVEOUDUVEUWNUWKUQURUSUTJBUVECUWQVAUVFUQIZUWLUWMUAUWJTUV
      TUVEUWKUVFUQVBUSVCUVCUWHUWJUWHUVCUWJULZUWHUVCUAUVFUVEKLZUWSUWHUVFUKKLZUKU
      VEKLZUWTUVCUWHUVFVDIZUXAUWRUXCUWHVEUVTUVFUQVFUSUXCUVFUKSLUXAUVFVGUVFUKVHV
      IVJUVCUKUVEVKZUXBAVLZUKUQIUXDUXBTVMUKUVEUQVNUSVIUVFUKUVEVOVPUVFUVEVQVRVSV
      TWAUVGQNUVGUHZUWIUWGTZUVFWBUXFUVGUKUWFWCIUWHUWGWDUXGUVGWEUVGUKUWFWFUWHUWG
      WGWHWIWJMHWKZUWGUWEVEWLDHUVGMWMUSWNUWDUVSDHUWDUWCUWCUWCRNZOPZSLUVSEUWBFEU
      XIGUGEUGFUGNKLGHWOWPZGUXKWQWRUWDUWCUVGUXJUVPSUWDWSZUWDUWCUVGUXIUVHOUXLUWC
      UVGRWTXAXBXCXDWJXEUVFUVGUVPXFVCUVPUVFUVHOPZUVNUVGUVFUCLUVHUVHUCLUVPUXMUCL
      UWAUVHUVGRUNZXGUVGUVFUVHUVHUVTUXNXHUTBUVEUVHCUWQUXNXIXJUVNUQIUVQUVRUAUVOT
      BUVMOUDUVFUVPUVNUQURUSXKUVLUVKUVOULZUVLUVCUVDUXOUVLUVCUAZUVDUAUVNUVFKLZUX
      OUXPUVMUVEKLZUVMXLUHZBXLUHZUAZULUXQUVDUVLUVMUVEUVEUBZKLUYBUVEUCLZUXRUVCUV
      HUVEUVEUWQUWQXMUVCUVEHIUXDUYCAXNUVCUXDUXEXOUVEXPVCUVMUYBUVEXQVPUVDUYAXRXS
      ZXLKLZUYEUYDXLXRXTUYDYAYBUVDUXTUYEUXSUXTUVDUYEUVDUYDBKLUXTUYEJUYDBKYCYDBX
      LUYDKYEYFYGYHYIUVMUVEBUVEUVHUWQUXNYJUWQCYKVPUVNUVFVQVRYLYMYNUVLUVIULZUVHU
      QIUVEUQIUVLUYFVEUXNUWQUVHUVEUQUQYOUTYPYQYRUVCULZUVEXLUHZUVJUYGAMYSZIZULUY
      HUYJUVCUYIHAUXHUYIHUHWLHMYTUSUUAUUBAMUUCVJUYHUVIUVDUYHUVIXLXRSLZUYKXRXLUU
      DUUEXRXRUKUUFUUGUUHUUIUYHUVEXLUVHXRSUYHWSUYHUVHXRRNZXRUYHUVGXRRUYHUVGXRQN
      ZXRUYHUVFXRQUYHUVFBXLOPXRUVEXLBOUUJBCUUKUULUUMXRUKIUYMXRUHUUFXRUUNUSUULUU
      MUYLXLXSZRNZXRXRUYNRUUOUUPXLHIUYOXRUHUUQXLUURUSUUSUULXBUUTUVAVRUVB $.
      $( [20-Mar-2013] $)
  $}

  ${
    $d A x $.  $d B x $.  $d f x y $.  $d f y z $.
    $( From ~ canth2 , we know that ` ( aleph `` 0 ) < ( 2 ^ om ) ` , but we
       cannot prove that ` ( 2 ^ om ) = ( aleph `` 1 ) ` (this is the Continuum
       Hypothesis), nor can we prove that it is less than any bound whatsoever
       (i.e. the statement ` ( aleph `` A ) < ( 2 ^ om ) ` is consistent for
       any ordinal ` A ` ).  However, we can prove that ` ( 2 ^ om ) ` is not
       equal to ` ( aleph `` om ) ` , nor ` ( aleph `` ( aleph `` om ) ) ` , on
       cofinality grounds, because by Konig's Theorem ~ konigth (in the form of
       ~ cfpwsdom ), ` ( 2 ^ om ) ` has uncountable cofinality, which
       eliminates limit alephs like ` ( aleph `` om ) ` .  (The first limit
       aleph that is _not_ eliminated is ` ( aleph `` ( aleph `` 1 ) ) ` ,
       which has cofinality ` ( aleph `` 1 ) ` .)  (Contributed by Mario
       Carneiro, 21-Mar-2013.) $)
    alephom $p |- ( card ` ( 2o ^m om ) ) =/= ( aleph ` om ) $=
      ( com csdm wbr wn c2o cmap co ccrd cfv cale wne sdomirr wceq c0 ccf cvv
      wcel cdom 2onn elisseti domrefg cfpwsdom mp2b aleph0 a1i opreq2i fveq2i
      eqeq1i biimpri fveq2d wlim limom alephsing ax-mp cfom eqtri syl6eq
      breq12d mpbii necon3bi ) AABCZDEAFGZHIZAJIZKALVAVCVDVCVDMZNJIZEVFFGZHIZOI
      ZBCZVAEPQEERCVJEASTZEPUANEVKUBUCVEVFAVIABVFAMVEUDUEVEVIVDOIZAVEVHVDOVHVDM
      VEVHVCVDVGVBHVFAEFUDUFUGUHUIUJVLAOIZAAUKVLVMMULAUMUNUOUPUQURUSUTUN $.
      $( [21-Mar-2013] $)
  $}


  $c InaccW $. $( Weakly inaccessible $)
  $c Inacc $. $( Strongly inaccessible $)

  $( The class of weak inaccessibles. $)
  cwina $a class InaccW $.

  $( The class of strong inaccessibles. $)
  cina $a class Inacc $.

  ${
    $d A x y $.
    $( An ordinal is weakly inaccessible iff it is a regular limit cardinal.
       Note that our definition allows ` om ` as a weakly inacessible
       cardinal. $)
    df-wina $a |- InaccW = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x E. z e. x y ~< z ) } $.

    $( An ordinal is strongly inaccessible iff it is a regular strong limit
       cardinal, which is to say that it dominates the powersets of every
       smaller ordinal. $)
    df-ina $a |- Inacc = { x | ( x =/= (/) /\ ( cf ` x ) = x /\
      A. y e. x ~P y ~< x ) } $.
  $}

  ${
    $d A x y z $.
    $( Conditions of weak inaccessibility. $)
    elwina $p |- ( A e. InaccW <-> ( A =/= (/) /\ ( cf ` A ) = A /\
        A. x e. A E. y e. A x ~< y ) ) $=
      ( vz cwina wcel cvv c0 wne ccf cfv wceq cv csdm wbr wrex wral w3a
      elisset fvex eleq1 mpbii 3ad2ant2 neeq1 wb fveq2 eqeq12 mpancom rexeq
      raleqbi1dv 3anbi123d df-wina elab2g pm5.21nii ) CEFCGFZCHIZCJKZCLZAMBMNOZ
      BCPZACQZRZCESURUPUOVAURUQGFUOCJTUQCGUAUBUCDMZHIZVCJKZVCLZUSBVCPZAVCQZRVBD
      CEGVCCLZVDUPVFURVHVAVCCHUDVEUQLVIVFURUEVCCJUFVEUQVCCUGUHVGUTAVCCUSBVCCUIU
      JUKDABULUMUN $.
      $( [22-Jun-2013] $)

    $( Conditions of strong inaccessibility. $)
    elina $p |- ( A e. Inacc <-> ( A =/= (/) /\ ( cf ` A ) = A /\
      A. x e. A ~P x ~< A ) ) $=
      ( vy cina wcel cvv c0 wne ccf cfv wceq cv cpw csdm wbr wral w3a elisset
      fvex eleq1 mpbii 3ad2ant2 neeq1 wb fveq2 eqeq12 mpancom breq2 raleqbi1dv
      3anbi123d df-ina elab2g pm5.21nii ) BDEBFEZBGHZBIJZBKZALMZBNOZABPZQZBDRUQ
      UOUNUTUQUPFEUNBISUPBFTUAUBCLZGHZVBIJZVBKZURVBNOZAVBPZQVACBDFVBBKZVCUOVEUQ
      VGUTVBBGUCVDUPKVHVEUQUDVBBIUEVDUPVBBUFUGVFUSAVBBVBBURNUHUIUJCAUKULUM $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is an ordinal. $)
    winaon $p |- ( A e. InaccW -> A e. On ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq cv csdm wbr wrex wral w3a con0
      elwina cfon eleq1 mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBKCKLMCANBAOZPAQEZ
      BCARUFUDUHUGUFUEQEUHASUEAQTUAUBUC $.
      $( [29-May-2013] $)

    $( Lemma for ~ inawina . $)
    inawinalem $p |- ( A e. On ->
        ( A. x e. A ~P x ~< A -> A. x e. A E. y e. A x ~< y ) ) $=
      ( con0 wcel cv cpw csdm wbr wrex wa cen cdom sdomdom breq2 rcla4ev
      sylan2 ondomen syl ensdomtr ad2ant2l wi sdomel ad2ant2r mpd visset
      canth2 pwex ensym cvv sdomentr ax-mp sylancr ad2antlr jca expcom
      reximdv2 ex ralimdv ) CDEZAFZGZCHIZVABFZHIZBCJZACUTVCVFUTVCKZVDVBLIZBDJZV
      FVGVBVDMIZBDJZVIVCUTVBCMIZVKVBCNVJVLBCDVDCVBMOPQBBVBRSVGVHVEBDCVDDEZVHKZV
      GVDCEZVEKVNVGKZVOVEVPVDCHIZVOVHVCVQVMUTVDVBCTUAVMUTVQVOUBVHVCVDCUCUDUEVHV
      EVMVGVHVAVBHIZVBVDLIZVEVAAUFZUGVDVBVAVTUHUIVDUJEVRVSKVEUBBUFVAVBVDUJUKULU
      MUNUOUPUQUEURUS $.
      $( [8-Jun-2013] $)

    $( Every strongly inaccessible cardinal is weakly inaccessible. $)
    inawina $p |- ( A e. Inacc -> A e. InaccW ) $=
      ( vx vy c0 wne ccf cfv wceq cv cpw csdm wbr wral w3a wrex cina wcel
      cwina con0 cfon eleq1 mpbii 3ad2ant2 idd inawinalem 3anim123d mpcom
      elina elwina 3imtr4i ) ADEZAFGZAHZBIZJAKLBAMZNZUKUMUNCIKLCAOBAMZNZAPQARQA
      SQZUPURUMUKUSUOUMULSQUSATULASUAUBUCUSUKUKUMUMUOUQUSUKUDUSUMUDBCAUEUFUGBAU
      HBCAUIUJ $.
      $( [29-May-2013] $)
  $}

  ${
    $( ` om ` is a strongly inaccessible cardinal.  (Many definitions of
       "inaccessible" explicitly disallow ` om ` as an inaccessible cardinal,
       but this choice allows us to reuse our results for inaccessibles for
       ` om ` .) $)
    omina $p |- om e. Inacc $=
      ( vx com cina wcel c0 wne ccf cfv wceq cv cpw csdm wbr wral elina peano1
      ne0i ax-mp cfom cfn wss ssid ssnnfi mpan2 pwfi sylib isfinite3 rgen
      mpbir3an ) BCDBEFZBGHBIAJZKZBLMZABNABOEBDUJPBEQRSUMABUKBDZULTDZUMUNUKTDZU
      OUNUKUKUAUPUKUBUKUKUCUDUKUEUFULUGUFUHUI $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x y $.
    $( A weakly inaccessible cardinal is a cardinal. $)
    winacard $p |- ( A e. InaccW -> ( card ` A ) = A ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq cv csdm wbr wrex wral w3a ccrd
      elwina cardcf fveq2 id eqeq12d mpbii 3ad2ant2 sylbi ) ADEAFGZAHIZAJZBKCKL
      MCANBAOZPAQIZAJZBCARUHUFUKUIUHUGQIZUGJUKASUHULUJUGAUGAQTUHUAUBUCUDUE $.
      $( [29-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( A weakly inaccessible cardinal is infinite. $)
    winainflem $p |- ( ( A =/= (/) /\ A e. On /\
        A. x e. A E. y e. A x ~< y ) -> om C_ A ) $=
      ( vz vw c0 wne con0 wcel cv csdm wbr wrex wral w3a com wss wn wceq csuc
      wo nn0suc simp1 necon2bi wa wi visset sucid eleq2 mpbiri adantl weq
      breq1 rexbidv breq2 cbvrexv syl6bb rcla4v syl cdom biimpa 3ad2antl2 wb
      nnon suceloni eleq1 biimparc sylan 3adant3 onelon simpl1 onsssuc
      syl11anc mpbird cvv ssdomg ax-mp domnsym 3syl nrexdv 3expia pm2.65d
      simp3 nsyl r19.23aiva jaoi con2i word ordom eloni 3ad2ant2 ordtri1
      sylancr ) CFGZCHIZAJZBJZKLZBCMZACNZOZPCQZCPIZRZXCXAXCCFSZCDJZTZSZDPMZUAXA
      RZDCUBXEXJXIXACFWNWOWTUCUDXHXJDPXFPIZXHUEZWTXAXLWTXFEJZKLZECMZXLXFCIZWTXO
      UFXHXPXKXHXPXFXGIXFDUGUHCXGXFUIUJUKWSXOAXFCADULZWSXFWQKLZBCMXOXQWRXRBCWPX
      FWQKUMUNXRXNBECWQXMXFKUOUPUQURUSXKXHWTXORXKXHWTOZXNECXSXMCIZUEZXMXFQZXMXF
      UTLZXNRYAYBXMXGIZXHXKXTYDWTXHXTYDCXGXMUIVAVBYAXMHIZXFHIZYBYDVCXSWOXTYEXKX
      HWOWTXKXGHIZXHWOXKYFYGXFVDZXFVEUSXHWOYGCXGHVFVGVHVICXMVJVHYAXKYFXKXHWTXTV
      KYHUSXMXFVLVMVNXMVOIYBYCUFEUGXMXFVOVPVQXMXFVRVSVTWAWBWNWOWTWCWDWEWFUSWGXA
      PWHCWHZXBXDVCWIWOWNYIWTCWJWKPCWLWMVN $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is infinite. $)
    winainf $p |- ( A e. InaccW -> om C_ A ) $=
      ( vx vy cwina wcel c0 wne ccf cfv wceq cv csdm wbr wrex wral w3a com wss
      elwina con0 cfon eleq1 mpbii winainflem syl3an2 sylbi ) ADEAFGZAHIZAJZBKC
      KLMCANBAOZPQARZBCASUIUGATEZUJUKUIUHTEULAUAUHATUBUCBCAUDUEUF $.
      $( [29-May-2013] $)

    $( A weakly inaccessible cardinal is a limit ordinal. $)
    winalim $p |- ( A e. InaccW -> Lim A ) $=
      ( cwina wcel com wss wlim winainf ccrd cfv wceq wb winacard cardlim
      sseq2 limeq bibi12d mpbii syl mpbid ) ABCZDAEZAFZAGTAHIZAJZUAUBKZALUDDUCE
      ZUCFZKUEAMUDUFUAUGUBUCADNUCAOPQRS $.
      $( [29-May-2013] $)

    $( A nontrivial weakly inaccessible cardinal is a limit aleph. $)
    winalim2 $p |- ( ( A e. InaccW /\ A =/= om ) ->
      E. x ( ( aleph ` x ) = A /\ Lim x ) ) $=
      ( vy vw vz cwina wcel com wne wa cv cale cfv wceq con0 wrex wlim wex
      ccrd winacard wss wb winainf cardalephex syl mpbid adantr df-rex simprr
      eqcomd c0 csuc cvv w3o simprl onzsl sylib wn simplr fveq2 aleph0 syl6eq
      eqtr sylan2 ex necon3ad sylc pm2.21d csdm wbr wral suceloni visset sucid
      alephord2i ee10 ad2antrl simplrr ad2antll eqtrd eleqtrrd ccf elwina
      simp3bi ad3antrrr breq1 rexbidv rcla4va syl11anc expr wi iscard simprbi
      ra4 3syl breq2d sylibd alephnbtwn2 pm3.21 mtoi syl6 imp nrexdv pm2.65d
      simpr a1i 3jaod mpd jca eximdv syl5bi ) BFGZBHIZJZBAKZLMZNZAOPZXPBNZXOQZJ
      ZARZXLXRXMXLBSMBNZXRBTZXLHBUAYCXRUBBUCABUDUEUFUGXRXOOGZXQJZARXNYBXQAOUHXN
      YFYAAXNYFYAXNYFJZXSXTYGBXPXNYEXQUIZUJYGXOUKNZXOCKZULZNZCOPZXOUMGZXTJZUNZX
      TYGYEYPXNYEXQUOCXOUPUQYGYIXTYMYOYGYIXTYGXQXMYIURYHXLXMYFUSXQYIBHXQYIBHNZY
      IXQXPHNYQYIXPUKLMHXOUKLUTVAVBBXPHVCVDVEVFVGVHYGYMXTYGYLCOYGYJOGZJYLYJLMZD
      KZVIVJZDBPZYGYRYLUUBYGYRYLJZJZYSBGEKZYTVIVJZDBPZEBVKZUUBUUDYSYKLMZBYRYSUU
      IGZYGYLYRYKOGYJYKGUUJYJVLYJCVMVNYJYKVOVPVQUUDBXPUUIXNYEXQUUCVRYLXPUUINYGY
      RXOYKLUTVSVTZWAXLUUHXMYFUUCXLBUKIBWBMBNUUHEDBWCWDWEUUGUUBEYSBUUEYSNUUFUUA
      DBUUEYSYTVIWFWGWHWIWJYGYRYLUUBURUUDUUADBUUDYTBGZUUAURZUUDUULYTUUIVIVJZUUM
      UUDUULYTBVIVJZUUNXLUULUUOWKZXMYFUUCXLYCUUODBVKZUUPYDYCBOGUUQDBWLWMUUODBWN
      WOWEUUDBUUIYTVIUUKWPWQUUNUUAUUAUUNJYJYTWRUUNUUAWSWTXAXBXCWJXDXCVHYOXTWKYG
      YNXTXEXFXGXHXIVEXJXKXH $.
      $( [29-May-2013] $)
  $}

  ${
    $d A x $.  $d A y z $.
    $( A nontrivial weakly inaccessible cardinal is a fixed point of the aleph
       function. $)
    winafp $p |- ( ( A e. InaccW /\ A =/= om ) -> ( aleph ` A ) = A ) $=
      ( vx vy vz cwina wcel com wne wa cv cale cfv wceq wlim wex winalim2 wss
      con0 cvv visset limelon mpan alephle syl ad2antll simprl sseqtrd ccf
      fveq2d alephsing c0 csdm wbr wrex wral elwina simp2bi ad2antrr 3eqtr3d
      cfle a1i eqsstr3d eqssd eqtr3d ex 19.23adv mpd ) AEFZAGHZIZBJZKLZAMZVKNZI
      ZBOAKLZAMZBAPVJVOVQBVJVOVQVJVOIZVLVPAVRVKAKVRVKAVRVKVLAVNVKVLQZVJVMVNVKRF
      ZVSVKSFVNVTBTVKSUAUBVKUCUDUEVJVMVNUFZUGVRAVKUHLZVKVRVLUHLZAUHLZWBAVRVLAUH
      WAUIVNWCWBMVJVMVKUJUEVHWDAMZVIVOVHAUKHWECJDJULUMDAUNCAUOCDAUPUQURUSWBVKQV
      RVKUTVAVBVCUIWAVDVEVFVG $.
      $( [29-May-2013] $)
  $}

  ${
    $d x y z $.
    $( The cardinality function is a function with domain the well-orderable
       sets.  Assuming AC, this is the universe. $)
    cardf $p |- card : _V --> On $=
      ( vx vy vz cvv con0 ccrd wf wfn cv cfv wcel wral ffnfv wfun cdm wceq
      df-fn cen wbr crab cint copab funopabeq df-card funeqi mpbir wex cab
      dmeqi dmopab wb abeq1 wrex numth2 intexrab mpbi isseti visset 2th mpgbir
      3eqtri mpbir2an cardon a1i rgen ) DEFGFDHZAIZFJEKZADLADEFMVFFNZFOZDPFDQVI
      BICIVGRSZCETUAZPZABUBZNABVLUCFVNABCUDZUEUFVJVNOVMBUGZAUHZDFVNVOUIVMABUJVQ
      DPVPVGDKZUKAVPADULVPVRBVLVKCEUMVLDKCVGUNVKCEUOUPUQAURUSUTVAVBVHADVHVRVGVC
      VDVEVB $.
      $( [6-Jun-2013] $)
  $}

  ${
    $d x y $.
    $( The beth function is strictly monotone.  This proof needs AC to ensure
       that each level of the hierarchy is well-orderable, and conversely this
       implies that every set is well-orderable, so this is an AC equivalent.
       This function is not strictly the beth function, but rather beth_A is
       the same as ` ( card `` ( R1 `` ( om +o A ) ) ) ` , since conventionally
       we start counting at the first infinite level, and ignore the finite
       levels. $)
    smobeth $p |- Smo ( card o. R1 ) $=
      ( vx vy ccrd cr1 ccom con0 cvv wf cardf wfn r1fnon dffn2 mpbi fco mp2an
      ordon cv wcel wa wel cfv csdm wbr r1sdom adantll wb fvex cardsdom sylibr
      wceq wfun ffun ax-mp fvco3 mp3an12 eleq12 syl2an adantr mpbird ex fdmi
      issmo ) ABCDEZFGFCHZFGDHZFFVCHIDFJVEKFDLMZFGFCDNOZPAQZFRZBQZFRZSZABTZVHVC
      UAZVJVCUAZRZVLVMSZVPVHDUAZCUAZVJDUAZCUAZRZVQVRVTUBUCZWBVKVMWCVIVJVHUDUEVR
      GRVTGRWBWCUFVHDUGVJDUGVRVTGGUHOUIVLVPWBUFZVMVIVNVSUJZVOWAUJZWDVKCUKZVEVIW
      EVDWGIGFCULUMZVFFGVHCDUNUOWGVEVKWFWHVFFGVJCDUNUOVNVSVOWAUPUQURUSUTFFVCVGV
      AVB $.
      $( [6-Jun-2013] $)
  $}


