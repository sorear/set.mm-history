$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cardinal numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c card $. $( Cardinal size function $)
  $c aleph $. $( Aleph function $)
  $c cf $. $( Cofinality function $)

  $( Extend class definition to include the cardinal size function. $)
  ccrd $a class card $.

  $( Extend class definition to include the aleph function. $)
  cale $a class aleph $.

  $( Extend class definition to include the cofinality function. $)
  ccf $a class cf $.

  ${
    $d x y z $.
    $( Define the cardinal number function.  The cardinal number of a set is
       the least ordinal number equinumerous to it.  In other words, it is the
       "size" of the set.  Definition of [Enderton] p. 197.  See ~ cardval for
       its value, ~ cardval2 for a simpler version of its value.  The principle
       theorem relating cardinality to equinumerosity is ~ carden .  Our
       notation is from Enderton.  Other textbooks often use a double bar over
       the set to express this function. $)
    df-card $a |- card = { <. x , y >. | y = |^| { z e. On | z ~~ x } } $.
  $}

  ${
    $d x y z $.
    $( Define the aleph function.  Our definition expresses Definition 12 of
       [Suppes] p. 229 in a closed form, from which we derive the recursive
       definition as theorems ~ aleph0 , ~ alephsuc , and ~ alephlim .
       The aleph function provides a one-to-one, onto mapping from the ordinal
       numbers to the infinite cardinal numbers.  Roughly, any aleph is the
       smallest infinite cardinal number whose size is strictly greater than
       any aleph before it. $)
    df-aleph $a |- aleph = rec ( { <. x , y >. | y = |^| { z e. On |
                   x ~< z } } , om ) $.
  $}

  ${
    $d x y z w v u $.
    $( Define the cofinality function.  Definition B of Saharon Shelah,
       _Cardinal Arithmetic_ (1994), p. xxx (Roman numeral 30).  See ~ cfval
       for its value and a description. $)
    df-cf $a |- cf = { <. x , y >. | ( x e. On /\
                y = |^| { z | E. w ( z = ( card ` w ) /\
                ( w (_ x /\ A. v e. x E. u e. w v (_ u ) ) } ) } $.
  $}

  ${
    $d x y z A $.
    $( The value of the cardinal number function with an ordinal number as its
       argument.  Unlike ~ cardval , this theorem does not require the Axiom of
       Choice. $)
    oncardval $p |- ( A e. On -> ( card ` A ) = |^| { x e. On | x ~~ A } ) $=
      ( vz vy con0 wcel cv cen wbr crab cint wceq copab cfv ccrd c0 wne wrex
      enrefg breq1 rcla4ev mpdan rabn0 sylibr wss ssrab2 oninton mpan syl
      breq2 rabbisdv inteqd fvopabg df-card fveq1i syl5eq ) BEFZBCGAGZDGZHIZAEJ
      ZKZLDCMZNZURBHIZAEJZKZBONUQVGEFZVDVGLUQVFPQZVHUQVEAERZVIUQBBHIZVJBESVEVKA
      BEURBBHTUAUBVEAEUCUDVFEUEVIVHVEAEUFVFUGUHUIDCBVBVGEEUSBLZVAVFVLUTVEAEUSBU
      RHUJUKULUMUBBOVCDCAUNUOUP $.
      $( [24-Nov-2003] $) $( [24-Nov-2003] $)

    $( The cardinal number of an ordinal number is an ordinal number.  Unlike
       ~ cardon , this theorem does not require the Axiom of Choice. $)
    oncardon $p |- ( A e. On -> ( card ` A ) e. On ) $=
      ( vx con0 wcel ccrd cfv cv cen wbr crab cint oncardval c0 wne cvv fvex
      syl6eqelr intex sylibr wss ssrab2 oninton mpan syl eqeltrd ) ACDZAEFZBGAH
      IZBCJZKZCBALZUFUIMNZUJCDZUFUJODULUFUJUGOUKAEPQUIRSUICTULUMUHBCUAUIUBUCUDU
      E $.
      $( [28-Jul-2004] $) $( [26-Jul-2004] $)

    $( Any ordinal number is equinumerous to its cardinal number.  Unlike
       ~ cardid , this theorem does not require the Axiom of Choice. $)
    oncardid $p |- ( A e. On -> ( card ` A ) ~~ A ) $=
      ( vx con0 wcel ccrd cfv cv cen wbr crab cint oncardval c0 wne cvv fvex
      syl6eqelr intex sylibr wss ssrab2 onint mpan syl eqeltrd breq1 elrab
      pm3.27bi ) ACDZAEFZBGZAHIZBCJZDZUJAHIZUIUJUMKZUMBALZUIUMMNZUPUMDZUIUPODUR
      UIUPUJOUQAEPQUMRSUMCTURUSULBCUAUMUBUCUDUEUNUJCDUOULUOBUJCUKUJAHUFUGUHUD
      $.
      $( [28-Jul-2004] $) $( [26-Jul-2004] $)

    $( The cardinal of an ordinal number is less than or equal to the
       ordinal number.  Proposition 10.6(3) of [TakeutiZaring] p. 85. $)
    cardonle $p |- ( A e. On -> ( card ` A ) (_ A ) $=
      ( vx con0 wcel ccrd cfv cv cen wbr crab cint oncardval wss enrefg wa
      breq1 elrab intss1 sylbir mpdan eqsstrd ) ACDZAEFBGZAHIZBCJZKZABALUBAAHIZ
      UFAMZACNUBUGOAUEDUHUDUGBACUCAAHPQAUERSTUA $.
      $( [25-Nov-2003] $) $( [22-Oct-2003] $)
  $}

  $( The cardinality of the empty set is the empty set. $)
  card0 $p |- ( card ` (/) ) = (/) $=
    ( c0 ccrd cfv wss wceq con0 wcel 0elon cardonle ax-mp ss0b mpbi ) ABCZADZMA
    EAFGNHAIJMKL $.
    $( [25-Nov-2003] $) $( [25-Oct-2003] $)

  $( The cardinality of a natural number is the number.  Corollary 10.23 of
     [TakeutiZaring] p. 90. $)
  cardnn $p |- ( A e. om -> ( card ` A ) = A ) $=
    ( com wcel ccrd cfv wn wceq csdm wbr cen sdomnen con0 nnont oncardid syl
    nsyl3 wpss wb oncardon word ordelpss eloni syl2an mpancom wo wss cardonle
    onsseleq mpbid elnn expcom eleq1a jaod mpd nnsdomo bitr4d mtbird ord ) ABCZ
    ADEZACZFUTAGZUSVAUTAHIZVCUTAJIZUSUTAKUSALCZVDAMZANOPUSVAUTAQZVCUSVEVAVGRZVF
    UTLCZVEVHASZUTTATVHVIVEUTAUAUTUBAUBUCUDOUTBCZUSVCVGRUSVAVBUEZVKUSVEVLVFVEUT
    AUFZVLAUGVIVEVMVLRVJUTAUHUDUIOZUSVAVKVBVAUSVKUTAUJUKABUTULUMUNUTAUOUDUPUQUS
    VAVBVNURUN $.
    $( [10-Jul-2004] $) $( [28-Oct-2003] $)

  $( The set of natural numbers is a cardinal number.  Theorem 18.11 of [Monk1]
     p. 133. $)
  cardom $p |- ( card ` om ) = om $=
    ( com ccrd cfv wcel wn wceq cen wbr con0 omelon oncardid ax-mp csdm nnsdom
    sdomnen syl mt2 wss wo cardonle oncardon onssel mpbi ori ) ABCZADZEUEAFZUFU
    EAGHZAIDZUHJAKLUFUEAMHUHEUENUEAOPQUFUGUEARZUFUGSUIUJJATLUEAUIUEIDJAUALJUBUC
    UDL $.
    $( [9-Dec-2003] $) $( [28-Oct-2003] $)

  ${
    $d x y z A $.
    $( The value of the cardinal number function.  Definition 10.4 of
       [TakeutiZaring] p. 85.  See ~ cardval2 for a simpler version of its
       value. $)
    cardval $p |- ( card ` A ) = |^| { x e. On | x ~~ A } $=
      ( vz vy cvv wcel ccrd cfv cv cen wbr con0 crab cint wceq copab wrex
      numth2 intexrab mpbi breq2 rabbisdv inteqd fvopabg mpan2 df-card fveq1i
      syl5eq wn c0 fvprc cab wa visset enref brprc mpbiri biantrud abbidv
      df-rab syl6reqr abid2 syl6eq inton eqtr4d pm2.61i ) BEFZBGHZAIZBJKZALMZNZ
      OVGBCIVIDIZJKZALMZNZODCPZHZVLVHVGVLEFZVRVLOVJALQVSABRVJALSTDCBVPVLEEVMBOZ
      VOVKVTVNVJALVMBVIJUAUBUCUDUEBGVQDCAUFUGUHVGUIZVHUJVLBGUKWAVLLNUJWAVKLWAVK
      VILFZAULZLWAWCWBVJUMZAULVKWAWBWDAWAVJWBWAVJVIVIJKVIAUNUOVIBJUPUQURUSVJALU
      TVAALVBVCUCVDVCVEVF $.
      $( [21-Oct-2003] $) $( [21-Oct-2003] $)

    $( The cardinal number of a set is an ordinal number.  Proposition 10.6(1)
       of [TakeutiZaring] p. 85.  Unlike Takeuti/Zaring's proposition, we need
       the Axiom of Choice (in ~ cardval ) because of our slightly different
       definition of of cardinal number. $)
    cardon $p |- ( card ` A ) e. On $=
      ( vx ccrd cfv cv cen wbr con0 crab cint cardval wss c0 wne wcel ssrab2
      cvv fvex eqeltrr intex mpbir oninton mp2an eqeltr ) ACDZBEAFGZBHIZJZHBAKZ
      UGHLUGMNZUHHOUFBHPUJUHQOUEUHQUIACRSUGTUAUGUBUCUD $.
      $( [25-Nov-2003] $) $( [22-Oct-2003] $)

    $( Any set is equinumerous to its cardinal number.  Proposition 10.5 of
       [TakeutiZaring] p. 85. $)
    cardid $p |- ( card ` A ) ~~ A $=
      ( vx ccrd cfv cv cen wbr con0 crab wcel cint cardval wss c0 wne ssrab2
      cvv fvex eqeltrr intex mpbir onint mp2an eqeltr breq1 elrab pm3.27bi
      ax-mp ) ACDZBEZAFGZBHIZJZUIAFGZUIULKZULBALZULHMULNOZUOULJUKBHPUQUOQJUIUOQ
      UPACRSULTUAULUBUCUDUMUIHJUNUKUNBUIHUJUIAFUEUFUGUH $.
      $( [25-Nov-2003] $) $( [22-Oct-2003] $)

    $( A set is a cardinal number iff it equals its own cardinal number.
       Proposition 10.9 of [TakeutiZaring] p. 85. $)
    oncard $p |- ( E. x A = ( card ` x ) <-> A = ( card ` A ) ) $=
      ( vy cv ccrd cfv wceq wex wss cen wbr cardid breq1 mpbiri entrt mpan
      con0 crab cint wcel wi cardon onintss ax-mp cardval syl5ss 3syl sseq1
      mpbird eleq1 cardonle syl eqssd 19.23aiv cvv fvex fveq2 eqeq2d cla4egv
      mpcom impbi ) BADZEFZGZAHZBBEFZGZVDVGAVDBVFVDBVFIVCVFIZVDBVBJKZVFVBJKZVHV
      DVIVCVBJKVBLBVCVBJMNVFBJKVIVJBLVFBVBOPVJCDZVBJKZCQRSZVFVCVFQTVJVMVFIUABUB
      VLVJCVFVKVFVBJMUCUDCVBUEUFUGBVCVFUHUIVDBQTZVFBIVDVNVCQTVBUBBVCQUJNBUKULUM
      UNBUOTZVGVEVGVOVFUOTBEUPBVFUOUJNVDVGABUOVBBGVCVFBVBBEUQURUSUTVA $.
      $( [11-Aug-2004] $) $( [22-Oct-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( No member of a cardinal number of a set is equinumerous to the set.
       Proposition 10.6(2) of [TakeutiZaring] p. 85. $)
    cardne $p |- ( A e. ( card ` B ) -> -. A ~~ B ) $=
      ( vx con0 wcel ccrd cfv cen wbr wn cardon onel wss cv crab cint breq1
      onintss cardval sseq1i syl6ibr wb ontri1 mpan sylibd con2d mpcom ) ADEZAB
      FGZEZABHIZJUIABKZLUHUKUJUHUKUIAMZUJJZUHUKCNZBHIZCDOPZAMUMUPUKCAUOABHQRUIU
      QACBSTUAUIDEUHUMUNUBULUIAUCUDUEUFUG $.
      $( [14-Dec-2003] $) $( [22-Oct-2003] $)
  $}

  $( Two sets are equinumerous iff their cardinal numbers are equal.  This
     important theorem expresses the essential concept behind "cardinality"
     or "size."  This theorem appears as Proposition 10.10 of [TakeutiZaring]
     p. 85, Theorem 7P of [Enderton] p. 197, and Theorem 9 of [Suppes] p. 242
     (among others).  The Axiom of Choice is required for its proof.

     The theory of cardinality can also be developed without AC by
     introducing "card" as a primitive notion and stating this theorem as an
     axiom, as is done with the axiom for cardinal numbers in [Suppes] p.
     111.  Finally, if we allow the Axiom of Regularity, we can avoid AC by
     defining the cardinal number of a set as the set of all sets
     equinumerous to it and having least possible rank (see ~ karden ). $)
  carden $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) = ( card ` B ) <-> A ~~ B ) ) $=
    ( wcel wa ccrd cfv wceq cen wbr wi breq2 cardid entrt mpan2 syl6bi ensymg
    mpi syl5com adantr wss mpan wn cardne con2i con0 wb cardon ontri1 mp2an
    sylibr syl syl6 a1i jcad eqss syl6ibr adantl impbid ) ACEZBDEZFAGHZBGHZIZAB
    JKZVAVEVFLVBVEAVCJKZVFVAVEVGAVDJKZVFVCVDAJMVHVDBJKZVFBNZAVDBOPQVAVCAJKZVGAN
    ZVCACRSTUAVBVFVELVAVBVFVCVDUBZVDVCUBZFVEVBVFVMVNVBVFBAJKZVMABDRVOVDAJKZVMVI
    VOVPVJVDBAOUCVPVDVCEZUDZVMVQVPVDAUEUFVCUGEZVDUGEZVMVRUHAUIZBUIZVCVDUJUKULUM
    UNVFVNLVBVFVCBJKZVNVKVFWCVLVCABOUCWCVCVDEZUDZVNWDWCVCBUEUFVTVSVNWEUHWBWAVDV
    CUJUKULUMUOUPVCVDUQURUSUT $.
    $( [14-Dec-2003] $) $( [22-Oct-2003] $)

  $( Only the empty set has cardinality zero. $)
  cardeq0 $p |- ( A e. B -> ( ( card ` A ) = (/) <-> A = (/) ) ) $=
    ( wcel ccrd cfv c0 wceq cen wbr cvv wb 0ex carden mpan2 card0 eqeq2i en0
    3bitr3g ) ABCZADEZFDEZGZAFHIZTFGAFGSFJCUBUCKLAFBJMNUAFTOPAQR $.
    $( [23-Aug-2004] $) $( [23-Apr-2004] $)

  ${
    $d x A $.  $d x B $.
    $( A singleton has cardinality one. $)
    cardsn $p |- ( A e. B -> ( card ` { A } ) = 1o ) $=
      ( wcel csn ccrd cfv c1o cen wbr wceq ensn1g cvv com wb snex 1onn carden
      mp2an sylibr cardnn ax-mp syl6eq ) ABCZADZEFZGEFZGUCUDGHIZUEUFJZABKUDLCGM
      CZUHUGNAOPUDGLMQRSUIUFGJPGTUAUB $.
      $( [20-Sep-2004] $) $( [23-Apr-2004] $)
  $}

  $( Two sets have the dominance relationship if their cardinalities have the
     subset relationship. $)
  carddomi $p |- ( A e. C -> ( ( card ` A ) (_ ( card ` B ) -> A ~<_ B ) ) $=
    ( wcel ccrd cfv wss cdom wbr wa cen endomtr cardid ensymg mpi con0 wi
    cardon ssdomg ax-mp syl2an domentr mpan2 syl ex ) ACDZAEFZBEFZGZABHIZUFUIJA
    UHHIZUJAUGKIZUGUHHIZUKUFUIAUGUHLUFUGAKIULAMUGACNOUGPDUIUMQARUGUHPSTUAUKUHBK
    IUJBMAUHBUBUCUDUE $.
    $( [25-Nov-2003] $) $( [22-Oct-2003] $)

  $( Two sets have the dominance relationship iff their cardinalities have the
     subset relationship.  Equation i of [Quine] p. 232. $)
  carddom $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) (_ ( card ` B ) <-> A ~<_ B ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr wi carddomi adantr csdm cen wo wn cardon
    onelss syl5 domnsym syl6 con2d con0 wb ontri1 mp2an syl6ibr adantl wceq
    carden eqimss syl6bir jaod brdom2 syl5ib impbid ) ACEZBDEZFZAGHZBGHZIZABJKZ
    USVDVELUTABCMNVAABOKZABPKZQVDVEVAVFVDVGUTVFVDLUSUTVFVCVBEZRZVDUTVHVFUTVHBAJ
    KZVFRUTVCVBIVJVHBADMVBVCASZTUABAUBUCUDVBUEEVCUEEVDVIUFVKBSVBVCUGUHUIUJVAVGV
    BVCUKVDABCDULVBVCUMUNUOABUPUQUR $.
    $( [22-Dec-2003] $) $( [22-Oct-2003] $)

  $( Two sets have the strict dominance relationship iff their cardinalities
     have the membership relationship.  Corollary 19.7(2) of [Eisenberg]
     p. 310. $)
  cardsdom $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) e. ( card ` B ) <-> A ~< B ) ) $=
    ( wcel wa ccrd cfv wss wne cdom wbr cen wn csdm carddom carden necon3abid
    anbi12d con0 wb cardon onelpsst mp2an brsdom 3bitr4g ) ACEBDEFZAGHZBGHZIZUH
    UIJZFZABKLZABMLZNZFUHUIEZABOLUGUJUMUKUOABCDPUGUNUHUIABCDQRSUHTEUITEUPULUAAU
    BBUBUHUIUCUDABUEUF $.
    $( [28-Dec-2003] $) $( [22-Oct-2003] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice. $)
  domtri $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr csdm wn carddom wb cardsdom ancoms negbid
    con0 cardon ontri1 mp2an syl5bb bitr3d ) ACEZBDEZFZAGHZBGHZIZABJKBALKZMZABC
    DNUGUIUHEZMZULUJUGUMUKUFUEUMUKOBADCPQRUHSEUISEUJUNOATBTUHUIUAUBUCUD $.
    $( [4-Jan-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of equinumerosity and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  Theorem 8 of [Suppes] p. 242. $)
  entri $p |- ( ( A e. C /\ B e. D ) -> ( A ~< B \/ A ~~ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen wo w3o wn cdom domtri biimprd brdom2 syl6ib con1d
    orrd df-3or sylibr ) ACEBDEFZABGHZABIHZJZBAGHZJUCUDUFKUBUEUFUBUFUEUBUFLZABM
    HZUEUBUHUGABCDNOABPQRSUCUDUFTUA $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance and strict dominance. $)
  entri2 $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen w3o cdom wo entri brdom2 orbi1i df-3or bitr4 sylibr
    ) ACEBDEFABGHZABIHZBAGHZJZABKHZUALZABCDMUDSTLZUALUBUCUEUAABNOSTUAPQR $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance.  This theorem is equivalent to the Axiom of
     Choice.  Part of Proposition 4.42(d) of [Mendelson] p. 275. $)
  entri3 $p |- ( ( A e. C /\ B e. D ) -> ( A ~<_ B \/ B ~<_ A ) ) $=
    ( wcel wa cdom wbr csdm wo entri2 sdomdom orim2i syl ) ACEBDEFABGHZBAIHZJOB
    AGHZJABCDKPQOBALMN $.
    $( [13-Nov-2004] $) $( [4-Jan-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Strict dominance of a set over a natural number is the same as dominance
       over its successor.  The proof uses AC and Infinity.  It is unclear if a
       proof without using these is possible, unlike the weaker versions
       ~ omsucdom , ~ sucdomi , and ~ finsucdom . $)
    sucdom $p |- ( ( A e. om /\ B e. C ) ->
                     ( A ~< B <-> suc A ~<_ B ) ) $=
      ( vx com wcel wa cdom wbr csdm wo csuc wb cvv omex entri2 mpan adantl wi
      sdomdomtr exp3a imp nnsdom sylan2 ancoms peano2b sylbi sdomdom domtr ex
      3syl adantr jcad pm5.1 syl6 cv cen wrex finsucdom isfinite2 syl5 jaod
      mpd ) AEFZBCFZGZEBHIZBEJIZKZABJIZALZBHIZMZVEVIVDENFVEVIOEBNCPQRVFVGVMVHVF
      VGVJVLGVMVFVGVJVLVEVDVGVJSZVEAEJIZVNVDVEVOVNVEVOVGVJAEBCTUAUBAUCUDUEVDVGV
      LSZVEVDVKEJIZVKEHIZVPVDVKEFVQAUFVKUCUGVKEUHVRVGVLVKEBUIUJUKULUMVJVLUNUOVD
      VHVMSVEVDBDUPUQIDEURZVMVHVDVSVMDABUSUJDBUTVAULVBVC $.
      $( [1-Oct-2004] $) $( [26-Jul-2004] $)
  $}

  ${
    $d x y z w v u t f A $.  $d x y z w v u t f B $.
    unxpdomlem.1 $e |- A e. V $.
    unxpdomlem.2 $e |- B e. V $.
    $( Lemma for ~ unxpdom . $)
    unxpdomlem $p |- ( ( 1o ~< A /\ 1o ~< B ) -> ( A u. B ) ~<_ ( A X. B ) ) $=
      ( vu vw vt vv vx vy vz vf cv wceq wn wrex cun cxp cdom wbr c1o csdm wcel
      wa wi cif copab2 crn wss wo eqeq1 ifbid ifeq2 ifeq2d eqtrd eqeq1d ifeq12
      sylanc rcla42ev eqid iftrue ax-mp id 3eqtr4a syl3an3 3exp com3l ad2antlr
      iffalse sylan9eqr exp4a imp3a adantlr pm2.61d adantl equcomi com24 imp
      syl5eq com23 adantr jaod elun syl5ib eqcom 2rexbii visset ifex elrnoprab
      bitr4 syl6ib ssrdv cvv unex ssdomg wfn xpex fnoprab2 fnrndomg mp2 domtr
      mpan2 3syl exp43 r19.23aivv r19.23advv c2o csuc com wb 1onn sucdom mp2an
      df-2o breq1i 2dom sylbi syl2an ) EMZFMZNZOZFAPEAPZGMZHMZNZOZHBPGBPZABQZAB
      RZSTZUAAUBTZUABUBTZYCYHYKYCYGYKGHBBYBYDBUCZYEBUCZUDZYGYKUEUEEFAAXSAUCZXTA
      UCZUDZYBYPYGYKYSYBUDZYPYGUDZUDZYIIMZAUCJMZBUCUDKMUUCXTNZUUDYENZXTUUDUFZUU
      FYEUUCUFZUFZNUDIJKUGZUHZUIZYIUUKSTZYKUUBLYIUUKUUBLMZYIUCZUUIUUNNZJBPIAPZU
      UNUUKUCZUUBUUNAUCZUUNBUCZUJUUQUUOUUBUUSUUQUUTUUAUUSUUQUEZYTUUAUUNXTNZUVAY
      OUVBUVAUEYNYGUUSYOUVBUUQUUSYOUVBUUQUUSYOUVBYEYENZXTYEUFZUVCYEUUNUFZUFZUUN
      NZUUQUVBUUPUVGUVBUUGUUFYEUUNUFZUFZUUNNZIJUUNYEABUUCUUNNZUUIUVIUUNUVKUUIUV
      BUUGUUHUFUVIUVKUUEUVBUUGUUHUUCUUNXTUKULUVKUVBUUHUVHUUGUUFUUCUUNYEUMUNUOUP
      ZUUFUVIUVFUUNUUGUVDNUVHUVENUVIUVFNUUFUVBUUGUVDUVHUVEUQUUFUUGUVCXTUUDUFUVD
      UUFUUFUVCXTUUDUUDYEYEUKZULUVCUUDYEXTUMUOUUFUUFUVCYEUUNUVMULURUPUSUVBUVDXT
      UVFUUNUVCUVDXTNYEUTUVCXTYEVAVBUVBUVDUVEVAUVBVCVDVEVFVGVHYNYGUVBOZUVAUEYOU
      USYNYGUDUVNUUQUUSYNYGUVNUUQUEUUSYNYGUVNUUQUUSYNYGUVNUDZUUQUUSYNUVBYFXTYDU
      FZYFYEUUNUFZUFZUUNNZUUQUVOUUPUVSUVJIJUUNYDABUVLUUDYDNZUVIUVRUUNUUGUVPNUVH
      UVQNUVIUVRNUVTUVBUUGUVPUVHUVQUQUVTUUGYFXTUUDUFUVPUVTUUFYFXTUUDUUDYDYEUKZU
      LYFUUDYDXTUMUOUVTUUFYFYEUUNUWAULURUPUSUVNYGUVRUVQUUNUVBUVPUVQVIYFYEUUNVIV
      JVEVFVKVLVGVMVNVOYTUUTUUQUEZUUAYTUUNYENZUWBYQYBUWCUWBUEZYRYQYBUWDYQUUTUWC
      YBUUQYQUUTUWCYBUUQYQUUTUWCYBUDZUUQYQUUTYAUWCXTUUNUFZUWCYEXSUFZUFZUUNNZUUQ
      UWEUUPUWIYAUUGUUFYEXSUFZUFZUUNNIJXSUUNABUUCXSNZUUIUWKUUNUWLUUIYAUUGUUHUFU
      WKUWLUUEYAUUGUUHUUCXSXTUKULUWLYAUUHUWJUUGUUFUUCXSYEUMUNUOUPUUDUUNNZUWKUWH
      UUNUUGUWFNZUWJUWGNUWKUWHNUWMYAUUGUWFUWJUWGUQUWMUUGUWCXTUUDUFUWFUWMUUFUWCX
      TUUDUUDUUNYEUKZULUWCUUDUUNXTUMUOZUWMUUFUWCYEXSUWOULURUPUSYBUWCUWHUWGUUNYA
      UWFUWGVIUWCUWGYEUUNUWCYEXSVALHVPUOVJVEVFVKVQVRVMYRUWCOZUWBUEYQYBYRUUTUWQU
      UQYRUUTUWQUUQYRUUTXTXTNZUWFUWCYEXTUFZUFZUUNNZUUQUWQUUPUXAUWRUUGUUFYEXTUFZ
      UFZUUNNIJXTUUNABUUEUUIUXCUUNUUEUUIUWRUUGUUHUFUXCUUEUUEUWRUUGUUHUUCXTXTUKU
      LUUEUWRUUHUXBUUGUUFUUCXTYEUMUNUOUPUWMUXCUWTUUNUWNUXBUWSNUXCUWTNUWMUWRUUGU
      WFUXBUWSUQUWPUWMUUFUWCYEXTUWOULURUPUSUWQUWFUUNUWTUWCXTUUNVIUWRUWTUWFNXTUT
      UWRUWFUWSVAVBVSVEVFVTVHVNWAWBUUNABWCWDUUQUUNUUINZJBPIAPUURUUPUXDIJABUUIUU
      NWEWFIJKABUUIUUNUUJUUEUUGUUHUUFXTUUDFWGJWGWHUUFYEUUCHWGIWGWHWHZUUJUTZWIWJ
      WKWLYIWMUCUULUUMUEABCDWNYIUUKWMWOVBUUMUUKYJSTZYKYJWMUCUUJYJWPUXGABCDWQIJK
      ABUUIUUJUXEUXFWRYJWMUUJWSWTYIUUKYJXAXBXCXDXEXFVRYLXGASTZYCYLUAXHZASTZUXHU
      AXIUCZAWMUCYLUXJXJXKCUAAWMXLXMXGUXIASXNXOWJEFACXPXQYMXGBSTZYHYMUXIBSTZUXL
      UXKBWMUCYMUXMXJXKDUABWMXLXMXGUXIBSXNXOWJGHBDXPXQXR $.
      $( [1-Oct-2004] $) $( [2-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Cross product dominates union for sets with cardinality greater than 1.
       Proposition 10.36 of [TakeutiZaring] p. 93. $)
    unxpdom $p |- ( ( 1o ~< A /\ 1o ~< B ) -> ( A u. B ) ~<_ ( A X. B ) ) $=
      ( vx vy cvv wcel wa c1o csdm wbr cun cxp cdom sdomex pm3.27d anim12i cv
      wi wceq breq2 anbi1d uneq1 xpeq1 breq12d imbi12d anbi2d uneq2 xpeq2
      visset unxpdomlem vtocl2g mpcom ) AEFZBEFZGHAIJZHBIJZGZABKZABLZMJZUOUMUPU
      NUOHEFZUMHANOUPVAUNHBNOPHCQZIJZHDQZIJZGZVBVDKZVBVDLZMJZRUOVEGZAVDKZAVDLZM
      JZRUQUTRCDABEEVBASZVFVJVIVMVNVCUOVEVBAHITUAVNVGVKVHVLMVBAVDUBVBAVDUCUDUEV
      DBSZVJUQVMUTVOVEUPUOVDBHITUFVOVKURVLUSMVDBAUGVDBAUHUDUEVBVDCUIDUIUJUKUL
      $.
      $( [1-Oct-2004] $) $( [15-Sep-2004] $)
  $}

  ${
    unxpdom2.1 $e |- A e. V $.
    unxpdom2.2 $e |- B e. V $.
    $( Corollary of ~ unxpdom . $)
    unxpdom2 $p |- ( ( 1o ~< A /\ B ~<_ A ) -> ( A u. B ) ~<_ ( A X. A ) ) $=
      ( cdom wbr c1o csdm cun cxp csn c0 domtr cen 0ex xpsnen ensymi domentr
      mpan2 com 1onn elisseti endom ax-mp wa cin wceq wne 1ne0 xpsndisj snex
      xpex p0ex undom mpan syl unxpdom cvv wcel wi sdomentr sylanc xpen mp2an
      syl2an ancoms ) BAEFZGAHFZABIZAAJZEFZVIAGKZJZALKZJZIZEFZVPVJEFZVKVGVHVIVP
      VJMVGBVOEFZVQVGAVONFZVSVOACALCOPZQZBAVORSAVMEFZVSVQAVMNFZWCVMACAGCGTUAUBP
      ZQZAVMUCUDWCVSUEVMVOUFLUGZVQGLUHWGUIAGALUJUDAVMBVOAVLCGUKULZDAVNCUMULZUNS
      UOUPVHVPVMVOJZEFZVRGVMHFZGVOHFZWKVHVMVOUQVHWDWLWFVMURUSVHWDUEWLUTWHGAVMUR
      VAUDSVHVTWMWBVOURUSVHVTUEWMUTWIGAVOURVAUDSVBWKWJVJNFZVRVMANFVOANFWNWEWAVM
      AVOAWHCWICVCVDVPWJVJRSUPVEVF $.
      $( [24-Jul-2006] $) $( [16-Sep-2004] $)
  $}

  ${
    $d x A $.
    $( Cross product dominates successor for set with cardinality greater
       than 1.  Proposition 10.38 of [TakeutiZaring] p. 93 (but generalized
       to arbitrary sets, not just ordinals, with a proof using AC). $)
    sucxpdom $p |- ( 1o ~< A -> suc A ~<_ ( A X. A ) ) $=
      ( vx cvv wcel c1o csdm wbr csuc cxp cdom sdomex pm3.27d cv wi wceq breq2
      suceq xpeq1 xpeq2 eqtrd breq12d imbi12d csn cun c0 wa cen wb visset 0ex
      xpsnen sdomen2 mp2an con0 1on elisseti ensn1 sdomen1 bitr sdomdom sylbir
      domrefg ax-mp domen2 mpbir cin wne 1ne0 xpsndisj snex xpex p0ex undom
      mpan2 mpan syl unxpdom sylancbr jca domtr xpen domentr 3syl df-suc
      syl5eqbr vtoclg mpcom ) ACDZEAFGZAHZAAIZJGZWIECDZWHEAKLEBMZFGZWNHZWNWNIZJ
      GZNWIWLNBACWNAOZWOWIWRWLWNAEFPWSWPWJWQWKJWNAQWSWQAWNIWKWNAWNRWNAASTUAUBWO
      WNWNUCZUDZWQWPJWOXAWNEUCZIZWNUEUCZIZUDZJGZXFXCXEIZJGZUFXAXHJGZXAWQJGZWOXG
      XIWOWTXEJGZXGWOWTXEFGZXLXMWTWNFGZWOWNCDZXEWNUGGZXMXNUHBUIZWNUEXQUJUKZXEWN
      WTCULUMWMWTEUGGXNWOUHEUNUOUPZWNXQUQWTEWNCURUMUSWTXEUTVAWNXCJGZXLXGXTWNWNJ
      GZXOYAXQWNCVBVCXOXCWNUGGZXTYAUHXQWNEXQXSUKZXCWNWNCVDUMVEXTXLUFXCXEVFUEOZX
      GEUEVGYDVHWNEWNUEVIVCWNXCWTXEWNXBXQEVJVKZWNVJWNXDXQVLVKZVMVNVOVPEXCFGZEXE
      FGZXIWOXCXEVQXOYBYGWOUHXQYCXCWNECULUMXOXPYHWOUHXQXRXEWNECULUMVRVSXAXFXHVT
      XJXHWQUGGZXKYBXPYIYCXRXCWNXEWNYEXQYFXQWAUMXAXHWQWBVNWCWNWDWEWFWG $.
      $( [24-Jul-2006] $) $( [3-Sep-2004] $)
  $}

  $( Strict dominance implies ordinal membership. $)
  sdomel $p |- ( ( A e. On /\ B e. On ) -> ( A ~< B -> A e. B ) ) $=
    ( con0 wcel csdm wbr wi wa wss cdom wn ssdomg adantr ontri1 domtri 3imtr3d
    a3d ancoms ) BCDZACDZABEFZABDZGSTHZUBUAUCBAIZBAJFZUBKUAKSUDUEGTBACLMBANBACC
    OPQR $.
    $( [21-Nov-2004] $) $( [4-Nov-2003] $)

  $( A set strictly dominates iff its cardinal strictly dominates. $)
  sdomsdomcard $p |- ( A ~< B <-> A ~< ( card ` B ) ) $=
    ( csdm wbr cvv wcel ccrd cfv sdomex pm3.27d wn c0 sdom0 fvprc breq2d
    mtbiri a3i cen wa wi fvex sdomentr ax-mp ex cardid ensymg mpi syl5com
    mpan2i impbid pm5.21nii ) ABCDZBEFZABGHZCDZULAEFUMABIJUMUOUMKZUOALCDAMUPUNL
    ACBGNOPQUMULUOULBUNRDZUOUMULUQUOUNEFULUQSUOTBGUAABUNEUBUCUDUMUNBRDZUQBUEZUN
    BEUFUGUHUMUOURULUSAUNBEUBUIUJUK $.
    $( [12-Jan-2004] $) $( [30-Oct-2003] $)

  $( The cardinality function is idempotent.  Proposition 10.11 of
     [TakeutiZaring] p. 85. $)
  cardidm $p |- ( card ` ( card ` A ) ) = ( card ` A ) $=
    ( cvv wcel ccrd cfv wceq cen wbr cardid wb fvex carden mpan mpbiri wn c0
    card0 fvprc fveq2d 3eqtr4a pm2.61i ) ABCZADEZDEZUCFZUBUEUCAGHZAIUCBCUBUEUFJ
    ADKUCABBLMNUBOZPDEPUDUCQUGUCPDADRZSUHTUA $.
    $( [19-Dec-2003] $) $( [30-Oct-2003] $)

  $( Cantor's theorem in terms of cardinals.  This theorem tells us that no
     matter how large a cardinal number is, there is a still larger cardinal
     number.  Theorem 18.12 of [Monk1] p. 133. $)
  canth3 $p |- ( A e. B -> ( card ` A ) e. ( card ` P~ A ) ) $=
    ( wcel ccrd cfv cpw csdm wbr canth2g cvv wb pwexg cardsdom mpdan mpbird )
    ABCZADEAFZDECZAQGHZABIPQJCRSKABLAQBJMNO $.
    $( [11-Jul-2004] $) $( [5-Nov-2003] $)

  ${
    $d x A $.
    $( An infinite cardinal is a limit ordinal.  Equivalent to Exercise 4 of
       [TakeutiZaring] p. 91. $)
    cardlim $p |- ( om (_ ( card ` A ) <-> Lim ( card ` A ) ) $=
      ( vx com ccrd cfv wss wlim cv csuc wceq con0 wrex wn wcel wa cen wbr wi
      sseq2 biimpd infensuc ex wb limom limsssuc ax-mp syl5ibr sylan9r breq2
      adantl sylibrd com3r imp visset sucid eleq2 mpbiri cardidm syl6eleqr
      cardne syl a1i pm2.65d nrexdv c0 wo peano1 ssel mpi n0i w3o word cardon
      onord ordzsl mpbi 3orass ori 3syl ord mpd limomss impbi ) CADEZFZWDGZWEWD
      BHZIZJZBKLZMWFWEWIBKWEWGKNZOZWIWGWDPQZWEWKWIWMRWKWIWEWMWKWIWEWMRWKWIOWEWG
      WHPQZWMWIWECWHFZWKWNWIWEWOWDWHCSTWKCWGFZWNWOWKWPWNWGUAUBCGWPWOUCUDCWGUEUF
      UGUHWIWMWNUCWKWDWHWGPUIUJUKUBULUMWIWMMZRWLWIWGWDDEZNWQWIWGWDWRWIWGWDNWGWH
      NWGBUNUOWDWHWGUPUQAURUSWGWDUTVAVBVCVDWEWJWFWEVEWDNZWDVEJZMWJWFVFZWEVECNWS
      VGCWDVEVHVIWDVEVJWTXAWTWJWFVKZWTXAVFWDVLXBWDAVMVNBWDVOVPWTWJWFVQVPVRVSVTW
      AWDWBWC $.
      $( [3-Jun-2004] $) $( [30-Oct-2003] $)
  $}

  $( A cardinal strictly dominates its members.  Equivalent to Proposition
     10.37 of [TakeutiZaring] p. 93 (use ~ cardsdom to obtain the exact
     proposition from this one). $)
  cardsdomel $p |- ( A e. On -> ( A ~< B <-> A e. ( card ` B ) ) ) $=
    ( con0 wcel ccrd cfv csdm wbr wss cdom wn ssdom2g wb cardon ontri1 mpan
    domtri 3imtr3d a3d cen wa onelss wi ax-mp syl cardidm eleq2i cardne sylbir
    jca brsdom sylibr impbid1 sdomsdomcard syl5bb ) ACDZABEFZGHZAUQDZABGHUPURUS
    UPUSURUPUQAIZUQAJHZUSKZURKZUQACLUQCDZUPUTVBMBNZUQAOPVDUPVAVCMVEUQACCQPRSUSA
    UQJHZAUQTHKZUAURUSVFVGUSAUQIZVFUQAVEUBVDVHVFUCVEAUQCLUDUEUSAUQEFZDVGVIUQABU
    FUGAUQUHUIUJAUQUKULUMABUNUO $.
    $( [13-Jan-2004] $) $( [2-Nov-2003] $)

  ${
    $d x A $.
    $( Two ways to express the property of being a cardinal number. $)
    iscard $p |- ( ( card ` A ) = A <-> ( A e. On /\ A. x e. A x ~< A ) ) $=
      ( ccrd cfv wceq con0 wcel wa cv csdm wbr wral cardon eleq1 mpbii
      pm4.71ri wss wb cardonle eqss baibr syl onelon cardsdomel ralbidva dfss3
      syl6rbbr bitr3d pm5.32i bitr ) BCDZBEZBFGZULHUMAIZBJKZABLZHULUMULUKFGUMBM
      UKBFNOPUMULUPUMBUKQZULUPUMUKBQZUQULRBSULURUQUKBTUAUBUMUPUNUKGZABLUQUMUOUS
      ABUMUNBGHUNFGUOUSRBUNUCUNBUDUBUEABUKUFUGUHUIUJ $.
      $( [14-Jan-2004] $) $( [2-Nov-2003] $)
  $}

  ${
    $d x y A $.
    $( Two ways to express the property of being a cardinal number.
       Definition 8 of [Suppes] p. 225. $)
    iscard2 $p |- ( ( card ` A ) = A <->
                  ( A e. On /\ A. x e. On ( A ~~ x -> A (_ x ) ) ) $=
      ( vy ccrd cfv wceq con0 wcel wa cv cen wbr wss wi wral cardon eleq1
      mpbii pm4.71ri cardonle biantrurd eqss syl6rbbr crab cint ensymg visset
      ensym impbid1 anbi2d breq1 elrab syl5bb imbi1d impexp syl6bb ralbidv2
      ssint cardval sseq2i bitrd pm5.32i bitr ) BDEZBFZBGHZVEIVFBAJZKLZBVGMZNZA
      GOZIVEVFVEVDGHVFBPVDBGQRSVFVEVKVFVEBVDMZVKVFVLVDBMZVLIVEVFVMVLBTUAVDBUBUC
      VFBCJZBKLZCGUDZUEZMZVKVLVFVIAVPOVKVRVFVIVJAVPGVFVGVPHZVINVGGHZVHIZVINVTVJ
      NVFVSWAVIVFVTVGBKLZIWAVSVFWBVHVTVFWBVHVGBGUFBVGAUGUHUIUJVOWBCVGGVNVGBKUKU
      LUMUNVTVHVIUOUPUQABVPURUMVDVQBCBUSUTUMVAVBVC $.
      $( [27-Nov-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x A $.
    $( An alternate version of the value of the cardinal number of a set.
       Compare ~ cardval .  This theorem could be used to give us a simpler
       definition of ` card ` in place of ~ df-card .  It apparently does not
       occur in the literature. $)
    cardval2 $p |- ( card ` A ) = { x e. On | x ~< A } $=
      ( cv csdm wbr con0 crab wcel wa cab ccrd cfv df-rab cardon onel pm4.71ri
      cardsdomel pm5.32i bitr4 abbii abid2 3eqtr2r ) ACZBDEZAFGUCFHZUDIZAJUCBKL
      ZHZAJUGUDAFMUHUFAUHUEUHIUFUHUEUGUCBNOPUEUDUHUCBQRSTAUGUAUB $.
      $( [12-Feb-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The collection of ordinal numbers dominated by a set is an ordinal
       number.  (In general, not all collections of ordinal numbers are
       ordinal.)  Theorem 56 of [Suppes] p. 227. $)
    ondomon $p |- ( A e. B -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz wcel cv cdom wbr con0 crab word wtr wss wa wi wal domtr anim2i
      anassrs onelon onelsst imp cvv visset ssdomg ax-mp syl jca sylan exp31
      com12 imp3a breq1 elrab 3imtr4g gen2 dftr2 mpbir ssrab2 ordon trssord
      mp3an cpw csdm wb wral elisset domsdomtr canth2g sylan2 expcom a1d
      r19.21aiv ss2rab sylibr ccrd cfv cardval2 fvex eqeltrr ssex elong 3syl
      mpbiri ) BCFZAGZBHIZAJKZJFZWILZWIMZWIJNJLWKWLDGZEGZFZWNWIFZOWMWIFZPZEQDQW
      RDEWOWPWQWOWNJFZWNBHIZOWMJFZWMBHIZOZWPWQWOWSWTXCWSWOWTXCPWSWOWTXCXAWMWNHI
      ZOWTXCWSWOOZXAXDWTXCXDWTOXBXAWMWNBRSTXEXAXDWNWMUAXEWMWNNZXDWSWOXFWNWMUBUC
      WMUDFXFXDPDUEWMWNUDUFUGUHUIUJUKULUMWHWTAWNJWGWNBHUNUOWHXBAWMJWGWMBHUNUOUP
      UCUQDEWIURUSWHAJUTVAWIJVBVCWFWIWGBVDZVEIZAJKZNZWIUDFWJWKVFWFWHXHPZAJVGZXJ
      WFBUDFZXLBCVHXMXKAJXMXKWGJFWHXMXHWHBXGVEIXHXMWGBXGVIBUDVJVKVLVMVNUHWHXHAJ
      VOVPWIXIXGVQVRXIUDAXGVSXGVQVTWAWBWIUDWCWDWE $.
      $( [27-Nov-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The class of ordinal numbers dominated by a set is a cardinal number.
       Theorem 59 of [Suppes] p. 228. $)
    ondomcard $p |- ( A e. B ->
                 ( card ` { x e. On | x ~<_ A } ) = { x e. On | x ~<_ A } ) $=
      ( vy wcel cvv cv cdom wbr con0 crab ccrd cfv wceq elisset cen wss wi
      wral wa ondomon wn csdm domsdomtr breq1 elrab pm3.27bi word eloni ordirr
      syl hbrab1 ax-17 hbbr elrabf biimpr ex mtod wb domtri con2bid mpancom
      mpbird syl2an sdomnen expcom con2d visset ensym syl5 adantr ontri1 sylan
      sylibrd r19.21aiva jca iscard2 sylibr ) BCEBFEZAGZBHIZAJKZLMWBNZBCOVSWBJE
      ZWBDGZPIZWBWEQZRZDJSZTWCVSWDWIABFUAZVSWHDJVSWEJEZTWFWEWBEZUBZWGVSWFWMRWKV
      SWEWBPIZWMWFVSWLWNWLVSWNUBZWLVSTWEWBUCIZWOWEBHIZBWBUCIZWPWLVSWEBWBUDWLWKW
      QWAWQAWEJVTWEBHUEUFUGVSWRWBBHIZUBZVSWDWTWJWDWSWBWBEZWDWBUHXAUBWBUIWBUJUKW
      DWSXAXAWDWSTWAWSADWBJWAADJULZWKAUMADWBBHXBWEHEAUMWEBEAUMUNVTWBBHUEUOUPUQU
      RUKWDVSWRWTUSWJWDVSTWSWRWBBJFUTVAVBVCVDWEWBVEUKVFVGWBWEDVHVIVJVKWDWKWGWMU
      SVSWBWEVLWJVMVNVOVPDWBVQVRUK $.
      $( [2-Dec-2004] $) $( [7-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The union of a set of cardinals is a cardinal.  Theorem 18.14 of [Monk1]
       p. 133. $)
    carduni $p |- ( A e. B ->
                  ( A. x e. A ( card ` x ) = x -> ( card ` U. A ) = U. A ) ) $=
      ( vy wcel cv ccrd cfv wceq wral cuni wa wss wn con0 ssonunit fveq2 id
      eqeq12d rcla4v cardon eleq1 mpbii syl6com ssrdv syl5 imp cardonle syl
      elirr wi wex eluni cdom wbr cvv wb uniexg visset carddom mpan bicomd
      sseq1 sylan9bb elssuni ssdomg ax-mp syl5bi ssel syl6 ex com13 syld com4r
      19.23aiv sylbi mtoi jca word eloni onord ordtri4 3syl mpbird ) BCEZAFZGHZ
      WFIZABJZBKZGHZWJIZWEWILZWLWKWJMZWKWJEZNZLZWMWNWPWMWJOEZWNWEWIWRWEBOMWRWIB
      CPWIDBODFZBEZWIWSGHZWSIZWSOEZWHXBAWSBWFWSIZWGXAWFWSWFWSGQXDRSTZXBXAOEXCWS
      UAXAWSOUBUCUDUEUFUGZWJUHUIWMWOWKWKEZWKUJWEWIWOXGUKWOWIWEXGWOWKWSEZWTLZDUL
      WIWEXGUKUKZDWKBUMXIXJDXHWTXJWTWIWEXHXGWTWIXBWEXHXGUKZUKXEWEXBWTXKWEXBWTXK
      UKWEXBLZWTWSWKMZXKXLWSWJUNUOZXMWTWEXNXAWKMZXBXMWEWJUPEZXNXOUQBCURXPXOXNWS
      UPEZXPXOXNUQDUSZWSWJUPUPUTVAVBUIXAWSWKVCVDWTWSWJMZXNWSBVEXQXSXNUKXRWSWJUP
      VFVGUIVHWSWKWKVIVJVKVLVMVNUGVOVPVLUGVQVRWMWRWJVSZWLWQUQZXFWJVTWKVSXTYAWKW
      JUAWAWKWJWBVAWCWDVK $.
      $( [19-Jan-2004] $) $( [1-Nov-2003] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( The indexed union of a set of cardinals is a cardinal. $)
    cardiun $p |- ( A e. C -> ( A. x e. A ( card ` B ) = B ->
                  ( card ` U_ x e. A B ) = U_ x e. A B ) ) $=
      ( vz vy wcel ccrd cfv wceq wral ciun wa cv wrex cab cuni cvv abrexexg
      visset eqeq1 rexbidv elab wi cardidm fveq2 id 3eqtr4a a1i r19.23aiv
      sylbi rgen carduni mpi syl fvex dfiun2 fveq2i 3eqtr4g adantr iuneq2
      adantl fveq2d 3eqtr3d ex ) BDGZCHIZCJABKZABCLZHIZVIJVFVHMZABVGLZHIZVLVJVI
      VFVMVLJVHVFENZVGJZABOZEPZQZHIZVRVMVLVFVQRGZVSVRJZAEBVGDSVTFNZHIZWBJZFVQKW
      AWDFVQWBVQGWBVGJZABOZWDVPWFEWBFTVNWBJVOWEABVNWBVGUAUBUCWEWDABWEWDUDANBGWE
      VGHIVGWCWBCUEWBVGHUFWEUGUHUIUJUKULFVQRUMUNUOVLVRHAEBVGCHUPUQZURWGUSUTVKVL
      VIHVHVLVIJVFABVGCVAVBZVCWHVDVE $.
      $( [21-Jan-2004] $) $( [3-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The smallest ordinal that strictly dominates a set is a
       cardinal. $)
    cardmin $p |- ( A e. B -> ( card ` |^| { x e. On | A ~< x } ) =
                      |^| { x e. On | A ~< x } ) $=
      ( vy wcel cv csdm wbr con0 crab cint wral wa ccrd cfv wceq wrex numthcor
      onintrab2 sylib cdom wn wi onelon ex syl breq2 elrab wss ssrab2 onnmin
      mpan sylbir con2d syli cvv wb visset domtri sylibrd ax-17 hbrab1 hbint
      hbbr onminsb jctird domsdomtr syl6 r19.21aiv jca iscard sylibr ) BCEZBAFZ
      GHZAIJZKZIEZDFZVQGHZDVQLZMVQNOVQPVMVRWAVMVOAIQZVRABCRZVOASTZVMVTDVQVMVSVQ
      EZVSBUAHZBVQGHZMVTVMWEWFWGVMWEBVSGHZUBZWFWEVMVSIEZWIVMVRWEWJUCWDVRWEWJVQV
      SUDUEUFWJWHWEWJWHWEUBZWJWHMVSVPEZWKVOWHAVSIVNVSBGUGUHVPIUIWLWKVOAIUJVPVSU
      KULUMUEUNUOVSUPEVMWFWIUQDURVSBUPCUSULUTVMWBWGWCVOWGAADBVQGVSBEAVAVSGEAVAA
      DVPVOADIVBVCVDVNVQBGUGVEUFVFVSBVQVGVHVIVJDVQVKVL $.
      $( [15-Jan-2004] $) $( [28-Oct-2003] $)
  $}

  ${
    $d x y $.
    $( The class of all cardinal numbers is not a set (i.e. is a proper class).
       Theorem 19.8 of [Eisenberg] p. 310. $)
    cardprc $p |- -. { x | ( card ` x ) = x } e. V $=
      ( vy cv ccrd cfv wceq cab cvv wcel cuni cpw canth3 wss wn cdom wbr fvex
      cardidm wb ax-17 hbab1 hbuni hbpw hbfv hbeq fveq2 id eqeq12d elabgf
      ax-mp mpbir elssuni ssdomg mp2 carddom mpan mpbiri syl5ssr con0 cardon
      ontri1 mp2an sylib pm2.65i uniexg mto ) ACZDEZVGFZAGZHIVJJZHIZVLVKDEZVKKZ
      DEZIZVKHLVLVOVMMZVPNZVLVODEZVMVOVLVSVMMZVOVKOPZVOHIZVOVKMZWAVNDQZVOVJIZWC
      WEVSVOFZVNRZWBWEWFSWDVIWFABVOHABVNDBCDIATZABVKABVJVIABUAUBUCUDZABBVSVOABV
      ODWHWIUDWIUEVGVOFZVHVSVGVOVGVODUFWJUGUHUIUJUKVOVJULUJVOVKHUMUNWBVLVTWASWD
      VOVKHHUOUPUQWGURVOUSIVMUSIVQVRSVNUTVKUTVOVMVAVBVCVDVJHVEVF $.
      $( [31-Oct-2004] $) $( [28-Oct-2003] $)
  $}

  ${
    $d x y z $.
    $( The aleph function is a function on the class of ordinal numbers. $)
    alephfnon $p |- aleph Fn On $=
      ( vy vx vz cale con0 wfn cv csdm wbr crab cint wceq copab com crdg
      rdgfnon wb df-aleph fneq1 ax-mp mpbir ) DEFZAGBGCGHICEJKLBAMZNOZEFZNUCPDU
      DLUBUEQBACREDUDSTUA $.
      $( [22-Oct-2003] $) $( [21-Oct-2003] $)

    $( The first infinite cardinal number, discovered by Georg Cantor in 1873,
       has the same size as the set of natural numbers ` om ` (and under our
       particular definition is also equal to it).  In the literature, the
       argument of the aleph function is often written as a subscript, and the
       first aleph is written ` aleph `_0.  Exercise 3 of [TakeutiZaring]
       p. 91.  Also Definition 12(i) of [Suppes] p. 228.  From Kuratowski and
       Mostowski, _Set Theory_, p. 95:  "Aleph...the first letter in the Hebrew
       alphabet...is also the first letter of the Hebrew word...(_einsoph_,
       meaning infinity), which is a cabbalistic appellation of the deity.  The
       notation is due to Cantor, who was deeply interested in mysticism." $)
    aleph0 $p |- ( aleph ` (/) ) = om $=
      ( vy vx vz c0 cale cfv cv csdm wbr con0 crab cint wceq copab com crdg
      df-aleph fveq1i omex rdg0 eqtr ) DEFDAGBGCGHICJKLMBANZOPZFODEUCBACQROUBST
      UA $.
      $( [21-Oct-2003] $) $( [21-Oct-2003] $)
  $}

  ${
    $d x y z w A $.
    $( Value of the aleph function at a limit ordinal.  Definition 12(iii) of
       [Suppes] p. 91. $)
    alephlim $p |- ( ( A e. B /\ Lim A ) -> ( aleph ` A ) =
                   U_ x e. A ( aleph ` x ) ) $=
      ( vz vy vw wcel wlim wa cv csdm wbr con0 crab cint wceq copab com crdg
      cfv ciun cale rdglim2a df-aleph fveq1i a1i iuneq2i 3eqtr4g ) BCGBHIBDJEJF
      JKLFMNOPEDQZRSZTABAJZUJTZUABUBTABUKUBTZUAARBCUIUCBUBUJEDFUDZUEABUMULUMULP
      UKBGUKUBUJUNUEUFUGUH $.
      $( [22-Oct-2003] $) $( [21-Oct-2003] $)

    $( An aleph is an ordinal number. $)
    alephon $p |- ( aleph ` A ) e. On $=
      ( vx vy vz vw con0 wcel cale cfv cv c0 csuc wceq fveq2 eleq1d com aleph0
      omelon eqeltr csdm wbr crab cint cvv wb wa ax-17 df-aleph breq1 rabbisdv
      inteqd rdgsucopab onintrab syl6rbbr ex ibd wn rdgsucopabn 0elon syl6eqel
      pm2.61d1 a1d wlim ciun wral visset alephlim mpan fvex iunon syl5bir
      tfinds cdm wfn alephfnon fndm ax-mp eleq2i negbii ndmfv sylbir pm2.61i )
      AFGZAHIZFGZBJZHIZFGZKHIZFGCJZHIZFGZWJLZHIZFGZWEBCAWFKMWGWIFWFKHNOWFWJMWGW
      KFWFWJHNOWFWMMWGWNFWFWMHNOWFAMWGWDFWFAHNOWIPFQRSWJFGZWOWLWPWKWFTUAZBFUBZU
      CZUDGZWOWPWTWOWPWTWTWOUEWPWTUFZWOWSFGWTXAWNWSFDCEPWJDJZWFTUAZBFUBZUCZWSUD
      HEJZPGDUGZXFWJGDUGZXFWSGDUGZDCBUHZXBWKMZXDWRXKXCWQBFXBWKWFTUIUJUKZULOWQBU
      MUNUOUPWTUQWNKFDCEPWJXEWSHXGXHXIXJXLURUSUTVAVBWFVCZWHCWFWKVDZFGWLCWFVEXMW
      GXNFWFUDGXMWGXNMBVFZCWFUDVGVHOCWFWKXOWJHVIVJVKVLWCUQZWDKFXPAHVMZGZUQWDKMX
      RWCXQFAHFVNXQFMVOFHVPVQVRVSAHVTWAUSUTWB $.
      $( [10-Dec-2003] $) $( [10-Nov-2003] $)

    $( Value of the aleph function at a successor ordinal.  Definition 12(ii)
       of [Suppes] p. 91. $)
    alephsuc $p |- ( A e. On -> ( aleph ` suc A ) =
                    |^| { x e. On | ( aleph ` A ) ~< x } ) $=
      ( vy vz vw con0 wcel cale cfv cv csdm wbr crab cint cvv csuc wceq wrex
      fvex numthcor ax-mp intexrab mpbi com ax-17 df-aleph breq1 rabbisdv
      inteqd rdgsucopab mpan2 ) BFGBHIZAJZKLZAFMZNZOGZBPHIUPQUNAFRZUQULOGURBHSA
      ULOTUAUNAFUBUCCDEUDBCJZUMKLZAFMZNUPOHEJZUDGCUEVBBGCUEVBUPGCUECDAUFUSULQZV
      AUOVCUTUNAFUSULUMKUGUHUIUJUK $.
      $( [11-Nov-2003] $) $( [22-Oct-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( Every aleph is a cardinal number.  Theorem 65 of [Suppes] p. 229. $)
    alephcard $p |- ( card ` ( aleph ` A ) ) = ( aleph ` A ) $=
      ( vx vy con0 wcel cale cfv ccrd wceq cv c0 csuc fveq2 fveq2d eqeq12d com
      cardom aleph0 fveq2i 3eqtr4 csdm wbr crab cint cvv fvex cardmin ax-mp
      alephsuc 3eqtr4a a1d wlim wral wa ciun wi visset cardiun adantl alephlim
      mpan adantr 3eqtr4d ex tfinds wn card0 cdm wfn alephfnon fndm eleq2i
      negbii ndmfv sylbir pm2.61i ) ADEZAFGZHGZVRIZBJZFGZHGZWBIZKFGZHGZWEICJZFG
      ZHGZWHIZWGLZFGZHGZWLIZVTBCAWAKIZWCWFWBWEWOWBWEHWAKFMZNWPOWAWGIZWCWIWBWHWQ
      WBWHHWAWGFMZNWROWAWKIZWCWMWBWLWSWBWLHWAWKFMZNWTOWAAIZWCVSWBVRXAWBVRHWAAFM
      ZNXBOPHGPWFWEQWEPHRSRTWGDEZWNWJXCWHWAUAUBBDUCUDZHGZXDWMWLWHUEEXEXDIWGFUFB
      WHUEUGUHXCWLXDHBWGUIZNXFUJUKWAULZWJCWAUMZWDXGXHUNZCWAWHUOZHGZXJWCWBXHXKXJ
      IZXGWAUEEZXHXLUPBUQZCWAWHUEURUHUSXIWBXJHXGWBXJIZXHXMXGXOXNCWAUEUTVAVBZNXP
      VCVDVEVQVFZKHGKVSVRVGXQVRKHXQAFVHZEZVFVRKIXSVQXRDAFDVIXRDIVJDFVKUHVLVMAFV
      NVOZNXTUJVP $.
      $( [22-Jan-2004] $) $( [25-Oct-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( No cardinal can be sandwiched between an aleph and its successor aleph.
       Theorem 67 of [Suppes] p. 229. $)
    alephnbtwn $p |- ( ( card ` B ) = B ->
                   -. ( ( aleph ` A ) e. B /\ B e. ( aleph ` suc A ) ) ) $=
      ( vx con0 wcel ccrd cfv wceq cale csuc wa wn wi csdm wbr wb eleq2
      alephon cardsdomel ax-mp syl5bb adantl cv crab cint alephsuc eleq2d
      biimpd breq2 onnminsb sylan9 con2d cardon eleq1 mpbii sylan2 sylbird
      imnan sylib ex c0 n0i cdm wfn alephfnon fndm eleq2i negbii ndmfv sylbir
      nsyl2 sucelon sylibr con3i a1d pm2.61i ) ADEZBFGZBHZAIGZBEZBAJZIGZEZKZLZM
      VQVSWFVQVSKZWAWDLZMWFWGWAVTBNOZWHVSWIWAPVQVSVTVREZWAWIVRBVTQVTDEWIWJPARVT
      BSTUAUBVQBDEZWIWHMVSVQWKKWDWIVQWDBVTCUCZNOZCDUDUEZEZWKWILVQWDWOVQWCWNBCAU
      FUGUHWMWICBWLBVTNUIUJUKULVSVRDEWKBUMVRBDUNUOUPUQWAWDURUSUTVQLWFVSWEVQWDVQ
      WAWDWBDEZVQWDWCVAHZWPWCBVBWPLWBIVCZEZLWQWSWPWRDWBIDVDWRDHVEDIVFTVGVHWBIVI
      VJVKAVLVMUBVNVOVP $.
      $( [25-Jan-2004] $) $( [10-Nov-2003] $)
  $}

  $( No set has equinumerosity between an aleph and its successor aleph. $)
  alephnbtwn2 $p |- -. ( ( aleph ` A ) ~< B /\ B ~< ( aleph ` suc A ) ) $=
    ( cvv wcel cale cfv csdm wbr csuc wa wn ccrd wceq cardidm alephnbtwn ax-mp
    wb con0 alephon cardsdomel a1i cardsdom mpan2 alephcard eleq2i syl5rbbr
    anbi12d mtbiri relsdom brrelexi adantl con3i pm2.61i ) BCDZAEFZBGHZBAIZEFZG
    HZJZKUNUTUOBLFZDZVAURDZJZVALFVAMVDKBNAVAOPUNUPVBUSVCUPVBQZUNUORDVEASUOBTPUA
    UNVAURLFZDZUSVCUNURRDVGUSQUQSBURCRUBUCVFURVAUQUDUEUFUGUHUTUNUSUNUPBURGUIUJU
    KULUM $.
    $( [8-Feb-2004] $) $( [3-Nov-2003] $)

  $( The power set of an aleph dominates the successor aleph.  (The
     Generalized Continuum Hypothesis says they are equinumerous.) $)
  alephsucpw $p |- ( aleph ` suc A ) ~<_ P~ ( aleph ` A ) $=
    ( csuc cale cfv cpw cdom wbr csdm wn fvex canth2 wi wa alephnbtwn2 imnan
    mpbir ax-mp cvv wcel wb pwex domtri mp2an ) ABZCDZACDZEZFGZUGUEHGZIZUFUGHGZ
    UJUFACJZKUKUJLUKUIMIAUGNUKUIOPQUERSUGRSUHUJTUDCJUFULUAUEUGRRUBUCP $.
    $( [29-Aug-2005] $) $( [27-Aug-2005] $)

  $( The set exponentiation of 2 to the aleph-zero has cardinality of at least
     aleph-one.  (If we were to assume the Continuum Hypothesis, their
     cardinalities would be the same.) $)
  aleph1 $p |- ( aleph ` 1o ) ~<_ ( 2o ^m ( aleph ` (/) ) ) $=
    ( c1o cale cfv c0 csuc c2o cm co cdom df-1o fveq2i cpw wbr alephsucpw cvv
    wcel cen wb oprex fvex pw2en domen2 mp2an mpbi eqbrtr ) ABCDEZBCZFDBCZGHZIA
    UFBJKUGUHLZIMZUGUIIMZDNUIOPUJUIQMUKULRFUHGSUHDBTUAUJUIUGOUBUCUDUE $.
    $( [8-Jul-2004] $) $( [7-Jul-2004] $)

  ${
    $d x y z A $.  $d x y z B $.
    $( Lemma for ~ alephordi . $)
    alephordlem1 $p |- ( A e. On -> ( aleph ` A ) ~< ( aleph ` suc A ) ) $=
      ( vx vy con0 wcel cale cfv cv csdm wbr crab cint csuc alephsuc wrex
      alephon numthcor ax-mp ax-17 hbrab1 hbint hbbr breq2 onminsb syl5breqr )
      ADEAFGZBHZIJZBDKZLZAMFGUFIBANUHBDOZUFUJIJZUFDEUKAPBUFDQRUHULBBCUFUJICHZUF
      EBSUMIEBSBCUIUHBCDTUAUBUGUJUFIUCUDRUE $.
      $( [9-Feb-2004] $) $( [26-Oct-2003] $)

    $( Lemma for ~ alephordi . $)
    alephordlem2 $p |- ( ( B e. V /\ Lim B ) ->
                       ( A e. B -> ( aleph ` A ) ~<_ ( aleph ` B ) ) ) $=
      ( vx cvv wcel wlim wa cale cfv wss cdom wbr cv ciun alephlim sseq2d
      fveq2 ssiun2s syl5bir con0 wi alephon ssdomg ax-mp syl6 ) BDEBFGZABEZAHIZ
      BHIZJZUHUIKLZUFUJUHCBCMZHIZNZJUGUFUIUNUHCBDOPCBUMAUHULAHQRSUHTEUJUKUAAUBU
      HUITUCUDUE $.
      $( [9-Feb-2004] $) $( [26-Oct-2003] $)

    $( Strict ordering property of the aleph function. $)
    alephordi $p |- ( B e. On ->
                    ( A e. B -> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
      ( vx vy cv wcel cale cfv csdm wbr wi c0 csuc wceq eleq2 fveq2 breq2d
      imbi12d noel pm2.21i con0 wo sdomtr alephordlem1 sylan2 expcom imim2d
      com23 breq1d syl5bir a1d com3r jaod visset elsuc2 syl5ib wlim wral cdom
      cen wn wa wss ciun cvv alephlim mpan sseq2d ssiun2s alephon ssdomg ax-mp
      syl6 limsuc alephordlem2 sylbid imp domnsym syl fvex ensym ensdomtr ex
      syl5 onelon limelon sylan syl5com mtod jcad brsdom syl6ibr tfinds ) ACEZF
      ZAGHZWNGHZIJZKZALFZWPLGHZIJZKADEZFZWPXCGHZIJZKZAXCMZFZWPXHGHZIJZKABFZWPBG
      HZIJZKCDBWNLNZWOWTWRXBWNLAOXOWQXAWPIWNLGPQRWNXCNZWOXDWRXFWNXCAOXPWQXEWPIW
      NXCGPQRWNXHNZWOXIWRXKWNXHAOXQWQXJWPIWNXHGPQRWNBNZWOXLWRXNWNBAOXRWQXMWPIWN
      BGPQRWTXBASTXCUAFZXIXGXKXSXDAXCNZUBXGXKKZXIXSXDYAXTXSXGXDXKXSXFXKXDXFXSXK
      XFXEXJIJZXKXSWPXEXJUCXCUDZUEUFUGUHXTXGXSXKXTXSXKKXGXTXKYBXSXTWPXEXJIAXCGP
      UIYCUJUKULUMXCADUNUOUPUHWNUQZWSXGDWNURYDWOWPWQUSJZWPWQUTJZVAZVBWRYDWOYEYG
      YDWOWPWQVCZYEYDYHWPDWNXEVDZVCWOYDWQYIWPWNVEFZYDWQYINCUNZDWNVEVFVGVHDWNXEA
      WPXCAGPVIUJWPUAFYHYEKAVJWPWQUAVKVLVMYDWOYGYDWOVBZYFWQAMZGHZIJZYLYNWQUSJZY
      OVAYDWOYPYDWOYMWNFZYPWNAVNYJYDYQYPKYKYMWNVOVGVPVQYNWQVRVSYFAUAFZYOYLYFWPY
      NIJZYOYRYFWQWPUTJZYSYOKWPWQWNGVTWAYTYSYOWQWPYNWBWCVSAUDWDWNUAFZWOYRYDWNAW
      EYJYDUUAYKWNVEWFVGWGWHWIWCWJWPWQWKWLUKWM $.
      $( [9-Feb-2004] $) $( [26-Oct-2003] $)
  $}

  $( Ordering property of the aleph function. $)
  alephord $p |- ( ( A e. On /\ B e. On ) ->
                 ( A e. B <-> ( aleph ` A ) ~< ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr wi alephordi adantl cdom cen wn wss wne
    con3d wb alephon domtri mp2an syl5ib adantr ontri1 sylibrd wceq fveq2
    eqeng ax-mp syl necon3bi a1i anim12d onelpsst brsdom impbid ) ACDZBCDZEZABD
    ZAFGZBFGZHIZUSVAVDJURABKLUTVBVCMIZVBVCNIZOZEZVAVDUTVHABPZABQZEVAUTVEVIVGVJU
    TVEBADZOZVIURVEVLJUSURVCVBHIZOZVLVEURVKVMBAKRVBCDZVCCDVEVNSATZBTVBVCCCUAUBU
    CUDABUEUFVGVJJUTVFABABUGVBVCUGZVFABFUHVOVQVFJVPVBVCCUIUJUKULUMUNABUOUFVBVCU
    PUCUQ $.
    $( [10-Feb-2004] $) $( [26-Oct-2003] $)

  $( Ordering property of the aleph function.  Theorem 8A(a) of [Enderton]
     p. 213 and its converse. $)
  alephord2 $p |- ( ( A e. On /\ B e. On ) ->
                  ( A e. B <-> ( aleph ` A ) e. ( aleph ` B ) ) ) $=
    ( con0 wcel wa cale cfv csdm wbr alephord ccrd wb alephon cardsdomel ax-mp
    alephcard eleq2i bitr syl6bb ) ACDBCDEABDAFGZBFGZHIZTUADZABJUBTUAKGZDZUCTCD
    UBUELAMTUANOUDUATBPQRS $.
    $( [11-Feb-2004] $) $( [3-Nov-2003] $)

  $( Ordering property of the aleph function.  Theorem 66 of [Suppes]
     p. 229. $)
  alephord2i $p |- ( B e. On ->
                   ( A e. B -> ( aleph ` A ) e. ( aleph ` B ) ) ) $=
    ( con0 wcel cale cfv wa onelon wi alephord2 biimpd ex imp3a mpcom ) BCDZABD
    ZAEFBEFDZACDZOPGQBAHROPQROPQIROGPQABJKLMNL $.
    $( [26-Feb-2004] $) $( [25-Oct-2003] $)

  $( Ordering property of the aleph function. $)
  alephord3 $p |- ( ( A e. On /\ B e. On ) ->
                  ( A (_ B <-> ( aleph ` A ) (_ ( aleph ` B ) ) ) $=
    ( con0 wcel wa wn cale cfv wss wb alephord2 ancoms negbid ontri1 alephon
    mp2an a1i 3bitr4d ) ACDZBCDZEZBADZFBGHZAGHZDZFZABIUDUCIZUAUBUETSUBUEJBAKLMA
    BNUGUFJZUAUDCDUCCDUHAOBOUDUCNPQR $.
    $( [12-Feb-2004] $) $( [11-Nov-2003] $)

  $( The aleph function is one-to-one. $)
  aleph11 $p |- ( ( A e. On /\ B e. On ) ->
                ( ( aleph ` A ) = ( aleph ` B ) <-> A = B ) ) $=
    ( con0 wcel wa wceq cale cfv wss alephord3 wb ancoms anbi12d eqss 3bitr4g
    bicomd ) ACDZBCDZEZABFZAGHZBGHZFZSABIZBAIZEUAUBIZUBUAIZETUCSUDUFUEUGABJRQUE
    UGKBAJLMABNUAUBNOP $.
    $( [29-Aug-2004] $) $( [3-Aug-2004] $)

  $( A set dominated by an aleph is strictly dominated by its successor
     aleph and vice-versa. $)
  alephsucdom $p |- ( B e. On ->
                    ( A ~<_ ( aleph ` B ) <-> A ~< ( aleph ` suc B ) ) ) $=
    ( cvv wcel con0 cale cfv cdom wbr csuc csdm wb wi wa domsdomtr ex
    alephordlem1 syl5com adantl wn fvex domtri mpan2 alephnbtwn2 imnan mpbir
    con2i syl5bir adantr impbid reldom brrelexi relsdom pm5.21ni a1d pm2.61i )
    ACDZBEDZABFGZHIZABJFGZKIZLZMUQURVCUQURNUTVBURUTVBMUQUTUSVAKIZVBURUTVDVBAUSV
    AOPBQRSUQVBUTMURUQUTUSAKIZTZVBUQUSCDUTVFLBFUAAUSCCUBUCVEVBVEVBTMVEVBNTBAUDV
    EVBUEUFUGUHUIUJPUQTVCURUTUQVBAUSHUKULAVAKUMULUNUOUP $.
    $( [11-Feb-2004] $) $( [3-Nov-2003] $)

  ${
    $d x A $.
    $( An alternate representation of a successor aleph.  Using this theorem
       we could define the aleph function with ` { z e. On | z ~<_ x } `
       in place of ` |^| { z e. On | x ~< z } ` in ~ df-aleph . $)
    alephsuc2 $p |- ( A e. On ->
                ( aleph ` suc A ) = { x e. On | x ~<_ ( aleph ` A ) } ) $=
      ( con0 wcel cv cale cfv cdom wbr crab csuc csdm alephsucdom rabbisdv
      ccrd alephcard cardval2 eqtr3 syl6reqr ) BCDZAEZBFGHIZACJUABKZFGZLIZACJZU
      DTUBUEACUABMNUDOGUDUFUCPAUDQRS $.
      $( [13-Feb-2004] $) $( [3-Nov-2003] $)
  $}

  $( Every aleph is greater than or equal to the set of natural numbers. $)
  alephgeom $p |- ( A e. On <-> om (_ ( aleph ` A ) ) $=
    ( con0 wcel com cale cfv wss c0 0ss wb 0elon alephord3 mpan mpbii aleph0
    syl5ssr cdm wn peano1 word ordom ord0 ordtri1 mp2an con2bii mpbi ndmfv
    sseq2d mtbiri a3i wfn wceq alephfnon fndm ax-mp syl6eleq impbi ) ABCZDAEFZG
    ZURHEFZUSDURHAGZVAUSGZAIHBCURVBVCJKHALMNOPUTAEQZBAVDCZUTVERZUTDHGZHDCZVGRSV
    GVHDTHTVGVHRJUAUBDHUCUDUEUFVFUSHDAEUGUHUIUJEBUKVDBULUMBEUNUOUPUQ $.
    $( [18-Feb-2004] $) $( [11-Nov-2003] $)

  $( Every aleph is a limit ordinal. $)
  alephislim $p |- ( A e. On <-> Lim ( aleph ` A ) ) $=
    ( con0 wcel com cale cfv wss wlim alephgeom ccrd cardlim alephcard sseq2i
    wceq wb limeq ax-mp 3bitr3 bitr ) ABCDAEFZGZTHZAIDTJFZGUCHZUAUBTKUCTDALZMUC
    TNUDUBOUEUCTPQRS $.
    $( [4-Jun-2004] $) $( [11-Nov-2003] $)

  ${
    $d x y A $.
    $( The argument of the aleph function is less than or equal to its value.
       Exercise 2 of [TakeutiZaring] p. 91.  (Later, in ~ alephfp2 , we will
       that equality can sometimes hold.) $)
    alephle $p |- ( A e. On -> A (_ ( aleph ` A ) ) $=
      ( vx vy cv cale cfv wss wceq id fveq2 sseq12d con0 wcel wral wa
      alephord2i imp wi onelon alephon ontr2 mpan2 syl mpan2d r19.20dva wn wb
      ontri1 elirr eleq1 rcla4cv mtoi syl5bir syld tfis3 ) BDZUPEFZGZCDZUSEFZGZ
      AAEFZGBCAUPUSHZUPUSUQUTVCIUPUSEJKUPAHZUPAUQVBVDIUPAEJKUPLMZVACUPNUSUQMZCU
      PNZURVEVAVFCUPVEUSUPMZOZVAUTUQMZVFVEVHVJUSUPPQVIUSLMZVAVJOVFRZUPUSSVKUQLM
      ZVLUPTZUSUTUQUAUBUCUDUEVEURUQUPMZUFZVGVEVMURVPUGVNUPUQUHUBVGVOUQUQMZUQUIV
      FVQCUQUPUSUQUQUJUKULUMUNUO $.
      $( [26-Feb-2004] $) $( [9-Nov-2003] $)

    $( Given any transfinite cardinal number ` A `, there is exactly one aleph
       that is equal to it.  Here we compute that aleph _explicitly_. $)
    cardaleph $p |- ( ( om (_ A /\ ( card ` A ) = A ) ->
                  A = ( aleph ` |^| { x e. On | A (_ ( aleph ` x ) } ) ) $=
      ( vy com wss ccrd cfv wceq wa cv cale con0 crab cint c0 csuc wrex wlim
      wo wcel cardon eleq1 mpbii alephle fveq2 sseq2d rcla4ev mpdan onintrab2
      sylib word eloni w3o ordzsl 3orass bitr 3syl adantl wi ax-17 hbrab1
      hbint hbfv hbss onminsb a1i aleph0 syl6eq sseq1d biimprd anim12d eqss
      syl6ibr com12 ancoms wn onnminsb visset sucid eleq2 mpbiri syl5 imp csdm
      wbr alephsuc sylan9eqr eleq2d biimpd cdom breq2 cvv wb fvex domtri mpan2
      sylibrd carddom syl sseq1 alephcard sseq2i syl6bb sylibd sylan9r mtod
      exp32 r19.23adv onelon sylan adantld mpcom alephon onelss nsyl nrexdv
      adantr ciun alephlim eliun mtbird ex jaod onsseleq mpbid ord syld mpd )
      DBEZBFGZBHZIZBAJZKGZEZALMZNZOHZYQCJZPZHZCLQZYQRZSZSZBYQKGZHZYKUUEYIYKBLTZ
      YQLTZUUEYKYJLTUUHBUAYJBLUBUCZUUHYOALQZUUIUUHBBKGZEZUUKBUDYOUUMABLYMBHYNUU
      LBYMBKUEUFUGUHZYOAUIUJZUUIYQUKZUUEYQULUUPYRUUBUUCUMUUECYQUNYRUUBUUCUOUPUJ
      UQURYLYRUUGUUDYKYIYRUUGUSYRYKYIIZUUGYRUUQBUUFEZUUFBEZIUUGYRYKUURYIUUSYKUU
      RUSYRYKUUHUUKUURUUJUUNYOUURAACBUUFYSBTAUTACYQKYSKTAUTACYPYOACLVAVBVCVDYMY
      QHYNUUFBYMYQKUEUFVEZUQVFYRUUSYIYRUUFDBYRUUFOKGDYQOKUEVGVHVIVJVKBUUFVLVMVN
      VOYKUUDUUGUSYIYKUUDBUUFTZVPZUUGYKUUBUVBUUCYKUUAUVBCLYKYSLTZUUAUVBYKUVCUUA
      IZIUVABYSKGZEZUVDUVFVPZYKUVCUUAUVGUVCYSYQTZUVGUUAYOUVFAYSYMYSHYNUVEBYMYSK
      UEUFVQZUUAUVHYSYTTYSCVRVSYQYTYSVTWAWBWCURUVDUVABUVEYMWDWEZALMNZTZYKUVFUVD
      UVAUVLUVDUUFUVKBUUAUVCUUFYTKGUVKYQYTKUEAYSWFWGWHWIYKUVLYJUVEFGZEZUVFYKUUH
      UVLUVNUSUUJUUHUVLBUVEWJWEZUVNUUHUVLUVEBWDWEZVPZUVOUVJUVPABYMBUVEWDWKVQUUH
      UVEWLTZUVOUVQWMYSKWNZBUVELWLWOWPWQUUHUVRUVNUVOWMUVSBUVELWLWRWPWQWSYKUVNBU
      VMEUVFYJBUVMWTUVMUVEBYSXAXBXCXDXEXFXGXHYKUUHUUCUVBUSUUJUUHUUCUVBUUHUUCIZU
      VABUVETZCYQQZUUHUWBVPUUCUUHUWACYQUUHUVHIZUVFUWAUVCUWCUVGUUIUVHUVCUUHYQYSX
      IUUOXJUVCUVHUVGUUHUVIXKXLUVEBYSXMXNXOXPXQUVTUVABCYQUVEXRZTUWBUVTUUFUWDBUU
      IUUCUUFUWDHUUHCYQLXSUUOXJWHCBYQUVEXTXCYAYBWSYCYKUUHUVBUUGUSUUJUUHUVAUUGUU
      HUURUVAUUGSZUUHUUKUURUUNUUTWSUUHUUFLTUURUWEWMYQXMBUUFYDWPYEYFWSYGURYCYH
      $.
      $( [27-Feb-2004] $) $( [9-Nov-2003] $)

    $( Every transfinite cardinal is an aleph and vice-versa.  Theorem 8A(b) of
       [Enderton] p. 213 and its converse. $)
    cardalephex $p |- ( om (_ A ->
                   ( ( card ` A ) = A <-> E. x e. On A = ( aleph ` x ) ) ) $=
      ( vy com wss ccrd cfv wceq cv cale con0 wrex crab cint wcel wa fveq2
      eqeq2d rcla4ev pm3.26 cardaleph sseq2d alephgeom syl6bbr mpbid sylanc ex
      wi alephcard id 3eqtr4a a1i r19.23aiv impbid1 ) DBEZBFGZBHZBAIZJGZHZAKLZU
      OUQVABCIJGECKMNZKOZBVBJGZHZVAUOUQPZUTVEAVBKURVBHUSVDBURVBJQRSVFUOVCUOUQTV
      FUODVDEVCVFBVDDCBUAZUBVBUCUDUEVGUFUGUTUQAKUTUQUHURKOUTUSFGUSUPBURUIBUSFQU
      TUJUKULUMUN $.
      $( [6-Nov-2004] $) $( [5-Nov-2003] $)
  $}

  ${
    $d x A $.
    $( Two ways to express the property of being a transfinite cardinal. $)
    isinfcard $p |- ( ( om (_ A /\ ( card ` A ) = A ) <-> A e. ran aleph ) $=
      ( vx cv cale cfv wceq con0 wrex crn wcel com wss ccrd wa eqcom rexbii
      wfn wb alephfnon fvelrnb ax-mp cardalephex pm5.32i sseq2 alephgeom biimp
      syl5cbir r19.23aiv pm4.71ri bitr4 3bitr4r ) BCZDEZAFZBGHZAUMFZBGHZADIJZKA
      LZAMEAFZNZUNUPBGUMAOPDGQURUORSBGADTUAVAUSUQNUQUSUTUQBAUBUCUQUSUPUSBGUPUSK
      UMLZULGJZAUMKUDVCVBULUEUFUGUHUIUJUK $.
      $( [18-Nov-2004] $) $( [9-Nov-2003] $)
  $}

  $( Two ways to express the property of being a cardinal number. $)
  iscard3 $p |- ( ( card ` A ) = A <-> A e. ( om u. ran aleph ) ) $=
    ( ccrd cfv wceq com wcel cale crn wo cun wn wss con0 word cardon eleq1
    mpbii eloni ordom ordtri2or mpan2 3syl ord wa isinfcard biimp expcom syld
    orrd cardnn bicomi pm3.27bi jaoi impbi elun bitr4 ) ABCZADZAEFZAGHZFZIZAEUT
    JFURVBURUSVAURUSKEALZVAURUSVCURAMFZANZUSVCIZURUQMFVDAOUQAMPQARVEENVFSAETUAU
    BUCVCURVAVCURUDZVAAUEZUFUGUHUIUSURVAAUJVAVCURVGVAVHUKULUMUNAEUTUOUP $.
    $( [19-Nov-2004] $) $( [9-Nov-2003] $)

  $( Two ways to express the class of all cardinal numbers, which consists
     of the finite ordinals in ` om ` plus the transfinite alephs. $)
  cardnum $p |- { x | ( card ` x ) = x } = ( om u. ran aleph ) $=
    ( com cale crn cun cv ccrd cfv wceq cab wcel iscard3 bicomi abbi2i eqcomi
    ) BCDEZAFZGHQIZAJRAPRQPKQLMNO $.
    $( [25-Nov-2004] $) $( [10-Sep-2004] $)

  ${
    $d x F $.  $d x A $.
    $( The union of the image of a mapping to cardinals is a cardinal.
       Proposition 11.16 of [TakeutiZaring] p. 104. $)
    carduniima $p |- ( A e. B -> ( F : A --> ( om u. ran aleph ) ->
                     U. ( F " A ) e. ( om u. ran aleph ) ) ) $=
      ( vx wcel com cale crn cun wf cima cuni ccrd cfv wceq cvv wfun funimaexg
      ffun sylan expcom cv wral carduni wfn ffn fnima syl frn eqsstrd sseld
      iscard3 syl6ibr r19.21aiv syl5 syli syl6ib ) ABEZAFGHIZCJZCAKZLZMNVBOZVBU
      SEUTURVAPEZVCUTURVDCQURVDUTCABRAUSCSTUAVDDUBZMNVEOZDVAUCVCUTDVAPUDUTVFDVA
      UTVEVAEVEUSEVFUTVAUSVEUTVACHZUSUTCAUEVAVGOAUSCUFACUGUHAUSCUIUJUKVEULUMUNU
      OUPVBULUQ $.
      $( [5-Dec-2004] $) $( [4-Nov-2004] $)

    $( If a mapping to cardinals has an infinite value, then the union of its
       image is an infinite cardinal.  Corollary 11.17 of [TakeutiZaring]
       p. 104. $)
    cardinfima $p |- ( A e. B -> ( ( F : A --> ( om u. ran aleph ) /\
         E. x e. A ( F ` x ) e. ran aleph ) -> U. ( F " A ) e. ran aleph ) ) $=
      ( wcel cvv com cale crn cun wf cv cfv wrex wa cima cuni wi elisset wss
      ccrd wceq isinfcard bicomi pm3.26bi wfn fnfvelrn ex fnima eleq2d sylibrd
      elssuni syl6 imp ffn sylan sylan9ssr anasss a1i carduniima iscard3
      syl6ibr adantrd jcad syl6ib exp4d r19.23adv imp3a syl ) BCEBFEZBGHIZJZDKZ
      ALZDMZVKEZABNZODBPZQZVKEZRBCSVJVMVQVTVJVMVQVTRVJVMOVPVTABVJVMVNBEZVPVTRRV
      JVMWAVPVTVJVMWAVPOZOZGVSTZVSUAMVSUBZOVTVJWCWDWEWCWDRVJVMWAVPWDVPVMWAOGVOV
      SVPGVOTZVOUAMVOUBZWFWGOVPVOUCUDUEDBUFZWAVOVSTZVMWHWAWIWHWAVOVREZWIWHWAVOD
      IZEZWJWHWAWLBVNDUGUHWHVRWKVOBDUIUJUKVOVRULUMUNBVLDUOUPUQURUSVJVMWEWBVJVMV
      SVLEWEBFDUTVSVAVBVCVDVSUCVEVFUNVGUHVHVI $.
      $( [26-Dec-2004] $) $( [4-Nov-2004] $)
  $}

  ${
    $d x y z $.
    $( Aleph is an order isomorphism of the class of ordinal numbers onto the
       class of infinite cardinals.  Definition 10.27 of [TakeutiZaring]
       p. 90. $)
    alephiso $p |- aleph Isom E , E ( On ,
                   { x | ( om (_ x /\ ( card ` x ) = x ) } ) $=
      ( vy vz con0 com cv wss ccrd cfv wceq wa cab cep cale wiso wf1o wbr wb
      wral df-iso wf1 wfo df-f1o wf wi f1fv wfn crn df-fo alephfnon wcel
      isinfcard bicomi abbi2i mpbir2an fof ax-mp aleph11 biimpd rgen2a
      alephord2 epel fvex epelc 3bitr4g ) DEAFZGVFHIVFJKZALZMMNODVHNPZBFZCFZMQZ
      VJNIZVKNIZMQZRZCDSBDSBCDVHMMNTVIDVHNUAZDVHNUBZDVHNUCVQDVHNUDZVMVNJZVJVKJZ
      UEZCDSBDSBCDVHNUFVRVSVRNDUGNUHZVHJDVHNUIUJVGAWCVGVFWCUKVFULUMUNUOZDVHNUPU
      QWBBCDVJDUKVKDUKKZVTWAVJVKURUSUTUOWDUOVPBCDWEVJVKUKVMVNUKVLVOVJVKVABCVBVM
      VNVJNVCVKNVCVDVEUTUO $.
      $( [28-Dec-2004] $) $( [3-Aug-2004] $)
  $}

  $( The class of all transfinite cardinal numbers (the range of the aleph
     function) is a proper class.  Proposition 10.26 of [TakeutiZaring]
     p. 90. $)
  alephprc $p |- -. ran aleph e. V $=
    ( vx cale crn cvv wcel com cun cv ccrd cfv wceq cab cardprc cardnum eleq1i
    mtbi omex unexg mpan mto ) BCZDEZFUAGZDEZAHZIJUEKALZDEUDAMUFUCDANOPFDEUBUDQ
    FUADDRST $.
    $( [11-Dec-2004] $) $( [11-Nov-2003] $)

  $( The class of transfinite cardinals (the range of the aleph function) is a
     subclass of the class of ordinal numbers. $)
  alephsson $p |- ran aleph (_ On $=
    ( vx cale crn con0 cv wcel com wss ccrd cfv wceq wa isinfcard cardon eleq1
    mpbii adantl sylbir ssriv ) ABCZDAEZTFGUAHZUAIJZUAKZLUADFZUAMUDUEUBUDUCDFUE
    UANUCUADOPQRS $.
    $( [30-Dec-2004] $) $( [11-Nov-2003] $)

  $( The union of the class of transfinite cardinals (the range of the aleph
     function) is the class of ordinal numbers. $)
  unialeph $p |- U. ran aleph = On $=
    ( cale crn cuni con0 wcel wn wceq cvv alephprc uniexb mtbi elisset mto
    word wo wss alephsson ssorduni ax-mp ordeleqon mpbi ori ) ABZCZDEZFUDDGZUEU
    DHEZUCHEUGIUCJKUDDLMUEUFUDNZUEUFOUCDPUHQUCRSUDTUAUBS $.
    $( [23-Jan-2005] $) $( [11-Nov-2003] $)

  ${
    $d x y z w v $.  $d z w v H $.
    alephfplem.1 $e |- H = ( rec ( { <. x , y >. | y = ( aleph ` x ) } ,
                       ( aleph ` (/) ) ) |` om ) $.
    $( Lemma for ~ alephfp . $)
    alephfplem1 $p |- ( H ` (/) ) e. ran aleph $=
      ( c0 cfv cale crn cv wceq copab crdg com cres fveq1i cvv wcel fvex fr0t
      ax-mp eqtr con0 wfn alephfnon 0elon fnfvelrn mp2an eqeltr ) ECFZEGFZGHZUI
      EBIAIGFJABKZUJLMNZFZUJECUMDOUJPQUNUJJEGRUJPULSTUAGUBUCEUBQUJUKQUDUEUBEGUF
      UGUH $.
      $( [20-Jan-2005] $) $( [6-Nov-2004] $)

    $( Lemma for ~ alephfp . $)
    alephfplem2 $p |- ( w e. om -> ( H ` suc w ) = ( aleph ` ( H ` w ) ) ) $=
      ( vz cv com wcel cfv cale cvv csuc wceq fvex c0 ax-17 copab crdg cres
      hbopab1 hbrdg hbres hbxfr hbfv fveq2 frsucopab mpan2 ) CGZHIUIDJZKJZLIUIM
      DJUKNUJKOABFPKJZUIAGZKJZUKLDFGZULIAQZUOUIIAQZAFUJKUOKIAQAFUIDAFDBGUNNZABR
      ZULSZHTEAFUTHAFULUSURABFUAUPUBUOHIAQUCUDUQUEUEEUMUJKUFUGUH $.
      $( [20-Jan-2005] $) $( [6-Nov-2004] $)

    $( Lemma for ~ alephfp . $)
    alephfplem3 $p |- ( v e. om -> ( H ` v ) e. ran aleph ) $=
      ( vw cv com wcel wceq cfv cale crn equid c0 csuc fveq2 eleq1d
      alephfplem1 a1i wi alephfplem2 con0 alephsson sseli wfn alephfnon
      fnfvelrn mpan syl syl5bir a1d finds2 mpi ) CGZHIBGZUPJZUODKZLMZIZBNUTODKZ
      USIZFGZDKZUSIZVCPZDKZUSIZUQCFUOOJURVAUSUOODQRUOVCJURVDUSUOVCDQRUOVFJURVGU
      SUOVFDQRVBUQABDESTVCHIZVEVHUAUQVIVHVDLKZUSIZVEVIVGVJUSABFDEUBRVEVDUCIZVKU
      SUCVDUDUELUCUFVLVKUGUCVDLUHUIUJUKULUMUN $.
      $( [20-Jan-2005] $) $( [6-Nov-2004] $)

    $( Lemma for ~ alephfp . $)
    alephfplem4 $p |- U. ( H " om ) e. ran aleph $=
      ( vz com cale crn cun wf cv cfv wcel wrex cima cuni wss wfn wral ffnfv
      wceq copab c0 crdg cres frfnom wb fneq1 ax-mp mpbir alephfplem3 rgen
      mpbir2an ssun2 fss mp2an peano1 alephfplem1 fveq2 eleq1d rcla4ev cvv wa
      wi omex cardinfima ) FFGHZIZCJZEKZCLZVGMZEFNZCFOPVGMZFVGCJZVGVHQVIVOCFRZV
      LEFSEFVGCTVPBKAKGLUAABUBZUCGLZUDFUEZFRZVRVQUFCVSUAVPVTUGDFCVSUHUIUJVLEFAB
      ECDUKULUMVGFUNFVGVHCUOUPUCFMUCCLZVGMZVMUQABCDURVLWBEUCFVJUCUAVKWAVGVJUCCU
      SUTVAUPFVBMVIVMVCVNVDVEEFVBCVFUIUP $.
      $( [20-Jan-2005] $) $( [5-Nov-2004] $)

    $( The aleph function has a fixed point.  Similar to Proposition 11.18 of
       [TakeutiZaring] p. 104, except that we construct an actual example of a
       fixed point rather than just showing its existence.  See ~ alephfp2
       for an abbreviated version just showing existence. $)
    alephfp $p |- ( aleph ` U. ( H " om ) ) = U. ( H " om ) $=
      ( vz vv com cima cuni cv cale cfv wceq con0 wrex crn wcel alephfplem4
      wss ccrd wa isinfcard cardalephex biimpa sylbir ax-mp wn alephle elirr
      wi wfun wb wfn copab c0 crdg cres frfnom fneq1 mpbir fnfun eluniima
      alephfplem3 alephsson sseli alephord2i 3syl csuc alephfplem2 peano2
      fnfvelrn mpan fnima syl6eleqr syl eqeltrrd elssuni sseld syld r19.23aiv
      sylbi eleq2 imbi12d mpbii mtoi anim12i word eloni alephon onord ordtri4
      mpan2 adantr mpbird eqeq2 adantl eqcomd fveq2d r19.23aiva ) CGHZIZEJZKLZM
      ZENOZXAKLZXAMZXAKPZQZXEABCDRXIGXASZXATLXAMZUAXEXAUBXJXKXEEXAUCUDUEUFXDXGE
      NXBNQZXDUAZXGXFXCMZXMXAXBKXMXBXAXMXBXAMZXBXCMZXMXPXBXCSZXBXCQZUGZUAZXLXQX
      DXSXBUHXDXRXCXCQZXCUIXDXBXAQZXCXAQZUJXRYAUJYBXBFJZCLZQZFGOZYCCUKZYBYGULCG
      UMZYHYIBJAJKLMABUNZUOKLZUPGUQZGUMZYKYJURCYLMYIYMULDGCYLUSUFUTZGCVAUFFGXBC
      VBUFYFYCFGYDGQZYFXCYEKLZQZYCYOYEXHQYENQYFYQUJABFCDVCXHNYEVDVEXBYEVFVGYOYP
      XAXCYOYPWTQYPXASYOYDVHZCLZYPWTABFCDVIYOYRGQZYSWTQYDVJYTYSCPZWTYIYTYSUUAQY
      NGYRCVKVLYIWTUUAMYNGCVMUFVNVOVPYPWTVQVOVRVSVTWAXDYBXRYCYAXAXCXBWBXAXCXCWB
      WCWDWEWFXLXPXTULZXDXLXBWGZUUBXBWHUUCXCWGUUBXCXBWIWJXBXCWKWLVOWMWNXDXOXPUL
      XLXAXCXBWOWPWNWQWRXDXGXNULXLXAXCXFWOWPWNWSUF $.
      $( [20-Jan-2005] $) $( [6-Nov-2004] $)
  $}

  ${
    $d x y z $.
    $( The aleph function has at least one fixed point.  Proposition 11.18 of
       [TakeutiZaring] p. 104.  See ~ alephfp for an actual example of a
       fixed point.  Compare the inequality ~ alephle that holds in
       general.  Note that if ` x ` is a fixed point, then
       ` aleph `` aleph `` aleph `` ` ... ` aleph `` x = x ` . $)
    alephfp2 $p |- E. x e. On ( aleph ` x ) = x $=
      ( vz vy cv cale cfv wceq copab c0 crdg com cres cima cuni con0 wcel wrex
      crn alephsson eqid alephfplem4 sselii alephfp fveq2 id eqeq12d rcla4ev
      mp2an ) BDCDEFGCBHIEFJKLZKMNZOPUJEFZUJGZADZEFZUMGZAOQEROUJSCBUIUITZUAUBCB
      UIUPUCUOULAUJOUMUJGZUNUKUMUJUMUJEUDUQUEUFUGUH $.
      $( [21-Jan-2005] $) $( [6-Nov-2004] $)
  $}

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function for
       nonzero arguments.  Theorem 64 of [Suppes] p. 229. $)
    alephval2 $p |- ( ( A e. On /\ (/) e. A ) -> ( aleph ` A ) =
                    |^| { x e. On | A. y e. A ( aleph ` y ) ~< x } ) $=
      ( vz cale cfv cv csdm wbr wral con0 crab wcel wn cint wceq c0 wa wss wi
      ssrab2 oneqmini ax-mp alephordi r19.21aiv alephon jctil breq2 ralbidv
      elrab sylibr adantr com cdom wo cvv omex visset entri3 mp2an ccrd wrex
      wb alephord ancoms breq1 cen cardid sdomen1 syl5rbbr sylan9bb fveq2
      breq1d rcla4v sdomirr sdomen2 syl5bbr mtbiri nsyli com12 adantl sylbird
      exp31 r19.23adv cardidm cardalephex mpbii syl5 carddom cardom sseq1i
      bitr3 syl5ib wne ne0i r19.2z ex domtr alephgeom ssdomg sylbi sylan2
      domnsym syl onelon exp32 imp rexnal syl6ib imp3a a1d com3r jaod mpi onel
      cardsdomel alephcard eleq2i syl6rbb ibi pm3.27bi con3i syl6 sylanc ) CEFZ
      BGZEFZAGZHIZBCJZAKLZMZDGZYKMZNZDYEJZYEYKOPZCKMZQCMZRZYKKSYLYPRYQTYJAKUADY
      EYKUBUCYRYLYSYRYEKMZYGYEHIZBCJZRYLYRUUCUUAYRUUBBCYFCUDUECUFZUGYJUUCAYEKYH
      YEPYIUUBBCYHYEYGHUHUIUJUKULYTYODYEYTYMYEMZYGYMHIZBCJZNZYOYTYMYEHIZUUHUUEY
      TUMYMUNIZYMUMUNIZUOZUUIUUHTZUMUPMZYMUPMZUULUQDURZUMYMUPUPUSUTYTUUJUUMUUKY
      RUUJUUMTYSYRUMYMVAFZSZUUMUUJYRUUQYHEFZPZAKVBZUUMUURYRUUTUUMAKYRYHKMZUUTUU
      MYRUVBRZUUTRUUIYHCMZUUHUVCUVDUUSYEHIZUUTUUIUVBYRUVDUVEVCYHCVDVEUUTUUQYEHI
      ZUVEUUIUUQUUSYEHVFUUOUUQYMVGIZUVFUUIVCUUPYMVHZUUQYMYEUPVIUTVJVKUUTUVDUUHT
      UVCUVDUUTUUHUVDUUGUUSYMHIZUUTUUFUVIBYHCYFYHPYGUUSYMHYFYHEVLVMVNUUTUVIUUSU
      USHIZUUSVOUUTUUSUUQHIZUVJUVIUUQUUSUUSHUHUUOUVGUVKUVIVCUUPUVHUUQYMUUSUPVPU
      TVQVRVSVTWAWBWCWDUURUUQVAFUUQPUVAYMWEAUUQWFWGWHUUJUMVAFZUUQSZUURUUNUUOUVM
      UUJVCUQUUPUMYMUPUPWIUTUVLUMUUQWJWKWLWMULUUKUUIYTUUHUUKYTUUHTUUIUUKYRYSUUH
      UUKYRYSUUHTYSUUKYRRZUUHYSUVNUUFNZBCVBZUUHYSCQWNZUVNUVPTCQWOUVQUVOBCJZUVPU
      VNUVQUVRUVPUVOBCWPWQUVNUVOBCUUKYRYFCMZUVOTUUKYRUVSUVOUUKYFKMZUVOYRUVSRUUK
      UVTRYMYGUNIZUVOUUKUMYGUNIZUWAUVTYMUMYGWRUVTUMYGSZUWBYFWSUUNUWCUWBTUQUMYGU
      PWTUCXAXBYMYGXCXDCYFXEXBXFXGUEWHXDUUFBCXHXIVTWQXJXKXLXMXNUUEUUIUUEYMKMZUU
      EUUIVCYEYMUUDXOUWDUUIYMYEVAFZMUUEYMYEXPUWEYEYMCXQXRXSXDXTWHYNUUGYNUWDUUGY
      JUUGAYMKYHYMPYIUUFBCYHYMYGHUHUIUJYAYBYCUEYD $.
      $( [28-Jan-2005] $) $( [15-Nov-2003] $)
  $}

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function: it is
       the least infinite cardinal different from all values at smaller
       arguments.  Definition of aleph in [Enderton] p. 212 and definition
       of aleph in [BellMachover] p. 490 . $)
    alephval3 $p |- ( A e. On -> ( aleph ` A ) = |^| { x | ( ( card ` x ) = x
                   /\ om (_ x /\ A. y e. A -. x = ( aleph ` y ) ) } ) $=
      ( vz cale cfv cv ccrd wceq com wss wn wral w3a cab wcel cint con0 wa wi
      cardon eleq1 mpbii 3ad2ant1 abssi oneqmini ax-mp alephcard a1i alephgeom
      biimp alephord2i elirr eleq2 mtbiri con2i syl6 r19.21aiv 3jca fvex fveq2
      id eqeq12d sseq2 eqeq1 negbid ralbidv 3anbi123d elab sylibr wrex wex
      alephord2 bicomd sylan9bbr biimpcd pm3.27 jcad exp4c com3r imp4b 19.22dv
      df-rex 3imtr4g cardalephex biimpac syl5 imp dfrex2 sylib nan mpbir ex
      visset df-3an bitr negbii syl6ibr sylanc ) CEFZAGZHFZXAIZJXAKZXABGZEFZIZL
      ZBCMZNZAOZPZDGZXKPZLZDWTMZWTXKQIZCRPZXKRKXLXPSXQTXJARXCXDXARPZXIXCXBRPXSX
      AUAXBXARUBUCUDUEDWTXKUFUGXRWTHFZWTIZJWTKZWTXFIZLZBCMZNZXLXRYAYBYEYAXRCUHU
      IXRYBCUJUKXRYDBCXRXECPZXFWTPZYDXECULYCYHYCYHXFXFPXFUMWTXFXFUNUOUPUQURUSXJ
      YFAWTCEUTXAWTIZXCYAXDYBXIYEYIXBXTXAWTXAWTHVAYIVBVCXAWTJVDYIXHYDBCYIXGYCXA
      WTXFVEVFVGVHVIVJXRXODWTXRXMWTPZXMHFZXMIZJXMKZSZXMXFIZLZBCMZSZLZXOXRYJYSXR
      YJSZYSTYTYNSZYQLZTUUAYOBCVKZUUBYTYNUUCYTYOBRVKZUUCYNYTXERPZYOSZBVLYGYOSZB
      VLUUDUUCYTUUFUUGBXRYJUUEYOUUGYJUUEXRYOUUGTYJUUEXRYOUUGYJUUEXRSZYOSZYGYOUU
      IYJYGYOYJYHUUHYGXMXFWTUBUUHYGYHXECVMVNVOVPUUIYOTYJUUHYOVQUIVRVSVTWAWBYOBR
      WCYOBCWCWDYMYLUUDBXMWEWFWGWHYOBCWIWJYTYNYQWKWLWMXNYRXNYLYMYQNZYRXJUUJAXMD
      WNXAXMIZXCYLXDYMXIYQUUKXBYKXAXMXAXMHVAUUKVBVCXAXMJVDUUKXHYPBCUUKXGYOXAXMX
      FVEVFVGVHVIYLYMYQWOWPWQWRURWS $.
      $( [1-Mar-2005] $) $( [16-Nov-2003] $)
  $}

  ${
    $d n x y z w A $.
    dominf.1 $e |- A e. V $.
    $( A nonempty set that is a subset of its union is infinite. $)
    dominf $p |- ( ( A =/= (/) /\ A (_ U. A ) -> om ~<_ A ) $=
      ( vx vz vw vy vn cv c0 wne cuni wss wa com cdom wbr wi wceq neeq1 id
      unieq sseq12d anbi12d breq2 imbi12d cpw cin crab copab crdg cres wf1
      eqid inf3lem6 cvv wcel visset pwex f1dom2g ax-mp csdm wn cen wrex pwfi
      biimp isfinite 3imtr4 con3i wb omex domtri mp2an 3syl vtocl ) CHZIJZVPVPK
      ZLZMZNVPOPZQAIJZAAKZLZMZNAOPZQCABVPARZVTWEWAWFWGVQWBVSWDVPAISWGVPAVRWCWGT
      VPAUAUBUCVPANOUDUEVTNVPUFZDHEHVPUGFHLEVPUHRFDUIZIUJNUKZULZNWHOPZWACFDEAAW
      JWIWIUMWJUMBBUNWHUOUPZWKWLQVPCUQZURZNWHUOWJUSUTWHNVAPZVBZVPNVAPZVBZWLWAWR
      WPVPGHZVCPGNVDZWHWTVCPGNVDZWRWPXAXBVPGVEVFGVPVGGWHVGVHVINUOUPZWMWLWQVJVKW
      ONWHUOUOVLVMXCVPUOUPWAWSVJVKWNNVPUOUOVLVMVHVNVO $.
      $( [30-Mar-2007] $) $( [25-Mar-2007] $)
  $}

