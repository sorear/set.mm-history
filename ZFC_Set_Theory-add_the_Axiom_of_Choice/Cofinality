$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Cofinality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u A $.
    $( A lemma used to simplify cofinality computations, showing the existence
       of the cardinal of an unbounded subset of a set ` A ` . $)
    cflem $p |- ( A e. B -> E. x E. y ( x = ( card ` y )
                  /\ ( y C_ A /\ A. z e. A E. w e. y z C_ w ) ) ) $=
      ( wcel cv wss wrex wral wa wex ccrd cfv wceq ssid sseq2 rcla4ev mpan2
      rgen sseq1 rexeq1 ralbidv anbi12d cla4egv mp2ani 19.41v fvex isseti
      mpbiran exbii excom bitr3i sylib ) EFGZBHZEIZCHZDHZIZDUQJZCEKZLZBMZAHUQNO
      ZPZVDLZBMAMZUPEEIZVADEJZCEKZVEEQVKCEUSEGUSUSIZVKUSQVAVMDUSEUTUSUSRSTUAVDV
      JVLLBEFUQEPZURVJVCVLUQEEUBVNVBVKCEVADUQEUCUDUEUFUGVEVHAMZBMVIVOVDBVOVGAMV
      DVGVDAUHAVFUQNUIUJUKULVHBAUMUNUO $.
      $( [2-May-2004] $) $( [24-Apr-2004] $)

    $( Value of the cofinality function.  Definition B of Saharon Shelah,
       _Cardinal Arithmetic_ (1994), p. xxx (Roman numeral 30).  The cofinality
       of an ordinal number ` A ` is the cardinality (size) of the smallest
       unbounded subset ` y ` of the ordinal number.  Unbounded means that for
       every member of ` A ` , there is a member of ` y ` that is at least as
       large.  Cofinality is a measure of how "reachable from below" an ordinal
       is. $)
    cfval $p |- ( A e. On -> ( cf ` A ) = |^| { x | E. y ( x = ( card ` y ) /\
                 ( y C_ A /\ A. z e. A E. w e. y z C_ w ) ) } ) $=
      ( vv vu con0 wcel cv ccrd cfv wceq wss wrex wral wa wex cab cint cvv ccf
      cflem intexab sylib sseq2 raleq1 anbi12d anbi2d exbidv abbidv inteqd
      df-cf fvopab4g mpdan ) EHIZAJBJZKLMZUQENZCJDJNDUQOZCEPZQZQZBRZASZTZUAIZEU
      BLVFMUPVDARVGABCDEHUCVDAUDUEFGEURUQFJZNZUTCVHPZQZQZBRZASZTVFHUAUBVHEMZVNV
      EVOVMVDAVOVLVCBVOVKVBURVOVIUSVJVAVHEUQUFUTCVHEUGUHUIUJUKULFGABCDUMUNUO $.
      $( [2-May-2004] $) $( [1-Apr-2004] $)
  $}

  ${
    $d x y z w v u $.
    $( Cofinality is a function on the class of ordinal numbers. $)
    cffnon $p |- cf Fn On $=
      ( vv vu vx vy vz vw con0 cv ccrd cfv wceq wss wrex wral wa wex cab cint
      ccf cvv wcel visset cflem ax-mp intexab mpbi df-cf fnopab2 ) ABGCHDHZIJKU
      IAHZLEHFHLFUIMEUJNOODPZCQRZSUKCPZULTUAUJTUAUMAUBCDEFUJTUCUDUKCUEUFABCDEFU
      GUH $.
      $( [25-Aug-2004] $) $( [24-Apr-2004] $)
  $}

  ${
    $d x y z w v A $.
    $( An upper bound on cofinality. $)
    cfub $p |- ( cf ` A ) C_
            |^| { x | E. y ( x = ( card ` y ) /\ ( y C_ A /\ A C_ U. y ) ) } $=
      ( vz vw con0 wcel ccf cfv cv ccrd wceq wss cuni wa wex cab cint wrex
      wral cfval wi ssel onelon ex sylan9r onelss syl6 imdistand ancomsd
      19.22dv eluni df-rex 3imtr4g r19.20sdv dfss3 syl5ib anim2d ss2abdv intss
      syl eqsstrd wn c0 0ss cdm wfn cffnon fndm ax-mp eleq2i notbii ndmfv
      sylbir sseq1d mpbiri pm2.61i ) CFGZCHIZAJBJZKILZVTCMZCVTNZMZOZOZBPZAQZRZM
      ZVRVSWAWBDJZEJZMZEVTSZDCTZOZOZBPZAQZRZWIABDECUAVRWHWSMWTWIMVRWGWRAVRWFWQB
      VRWEWPWAVRWBWDWOVRWBWDWOUBVRWBOZWKWCGZDCTWOWDXAXBWNDCXAWKWLGZWLVTGZOZEPXD
      WMOZEPXBWNXAXEXFEXAXDXCXFXAXDXCWMXAXDWLFGZXCWMUBWBXDWLCGZVRXGVTCWLUCVRXHX
      GCWLUDUEUFWLWKUGUHUIUJUKEWKVTULWMEVTUMUNUODCWCUPUQUEUIURUKUSWHWSUTVAVBVRV
      CZWJVDWIMWIVEXIVSVDWIXICHVFZGZVCVSVDLXKVRXJFCHFVGXJFLVHFHVIVJVKVLCHVMVNVO
      VPVQ $.
      $( [29-Aug-2004] $) $( [25-Apr-2004] $)

    $( Value of the cofinality function at a limit ordinal.  Part of Definition
       of cofinality of [Enderton] p. 257. $)
    cflim $p |- ( ( A e. B /\ Lim A ) -> ( cf ` A ) =
           |^| { x | E. y ( x = ( card ` y ) /\ ( y C_ A /\ A = U. y ) ) } ) $=
      ( vz vw vv cvv wcel wlim ccf cfv cv ccrd wceq wss cuni wa wex cab cint
      wrex wral wi csuc limsuc biimpd sseq1 rexbidv rcla4v visset sucssel
      ax-mp r19.22si eluni2 sylibr syl6com syl9 r19.21adv dfss3 syl6ibr adantr
      limuni sseq2d uniss syl5bir imp jctird eqss ex imdistand anim2d 19.22dv
      ss2abdv intss syl adantl con0 limelon cfval sseqtr4d cfub eqimss anim2i
      19.22i ss2abi sstri jctil elisset sylan ) CHIZCJZCKLZAMBMZNLOZWNCPZCWNQZO
      ZRZRZBSZATZUAZOZCDIWKWLRZWMXCPZXCWMPZRXDXEXGXFXEXCWOWPEMZFMZPZFWNUBZECUCZ
      RZRZBSZATZUAZWMWLXCXQPZWKWLXPXBPXRWLXOXAAWLXNWTBWLXMWSWOWLWPXLWRWLWPXLWRU
      DWLWPRZXLCWQPZWQCPZRWRXSXLXTYAWLXLXTUDWPWLXLGMZWQIZGCUCXTWLXLYCGCWLYBCIZY
      BUEZCIZXLYCWLYDYFCYBUFUGYFXLYEXIPZFWNUBZYCXKYHEYECXHYEOXJYGFWNXHYEXIUHUIU
      JYHYBXIIZFWNUBYCYGYIFWNYBHIYGYIUDGUKYBXIHULUMUNFYBWNUOUPUQURUSGCWQUTVAVBW
      LWPYAWLYAWQCQZPWPWLCYJWQCVCVDWNCVEVFVGVHCWQVIVAVJVKVLVMVNXPXBVOVPVQXECVRI
      WMXQOCHVSABEFCVTVPWAWMWOWPXTRZRZBSZATZUAZXCABCWBXBYNPYOXCPXAYMAWTYLBWSYKW
      OWRXTWPCWQWCWDWDWEWFXBYNVOUMWGWHWMXCVIUPCDWIWJ $.
      $( [1-Sep-2004] $) $( [26-Apr-2004] $)
  $}

  ${
    $d x y $.
    $( Value of the cofinality function at 0.  Exercise 2 of [TakeutiZaring]
       p. 102. $)
    cf0 $p |- ( cf ` (/) ) = (/) $=
      ( vx vy c0 ccf cfv wss wceq cv ccrd cuni wa wex cab cint cfub csn 0ss
      biantru ss0b bitr3i anbi2i ancom bitri exbii 0ex fveq2 eqeq2d ceqsexv
      card0 eqeq2i 3bitri abbii df-sn eqtr4i inteqi intsn eqtri sseqtri mpbi )
      CDEZCFUTCGUTAHZBHZIEZGZVBCFZCVBJZFZKZKZBLZAMZNZCABCOVLCPZNCVKVMVKVACGZAMV
      MVJVNAVJVBCGZVDKZBLVACIEZGZVNVIVPBVIVDVOKVPVHVOVDVHVEVOVGVEVFQRVBSTUAVDVO
      UBUCUDVDVRBCUEVOVCVQVAVBCIUFUGUHVQCVAUIUJUKULACUMUNUOCUEUPUQURUTSUS $.
      $( [30-Aug-2004] $) $( [16-Apr-2004] $)
  $}

  ${
    $d x y z w v u A $.
    $( Cofinality is a cardinal number.  Proposition 11.11 of [TakeutiZaring]
       p. 103. $)
    cardcf $p |- ( card ` ( cf ` A ) ) = ( cf ` A ) $=
      ( vx vy vz vw vv con0 wcel ccf cfv ccrd wceq cv wss wrex wral wa wex cab
      cint cfval c0 wne onint visset eqeq1 anbi1d exbidv elab fveq2 cardidm
      syl6eq eqeq2 mpbird adantr 19.23aiv sylbi cardon syl6eqelr ssriv cvv
      fvex intex sylibr sylancr eqeltrd id eqeq12d vtoclga syl wn cdm wfn
      cffnon fndm ax-mp eleq2i notbii ndmfv sylbir card0 3eqtr4a pm2.61i ) AGHZ
      AIJZKJZWELZWDWEBMZCMZKJZLZWIANDMEMNEWIODAPQZQZCRZBSZHWGWDWEWOTZWOBCDEAUAZ
      WOGNWOUBUCZWPWOHWDWOUDFWOGFMZWOHZWSWSKJZGWTWSWJLZWLQZCRZXAWSLZWNXDBWSFUEW
      HWSLZWMXCCXFWKXBWLWHWSWJUFUGUHUIXCXECXBXEWLXBXEXAWJLXBXAWJKJWJWSWJKUJWIUK
      ULWSWJXAUMUNUOUPUQZWSURUSUTWDWPVAHWRWDWPWEVAWQAIVBUSWOVCVDVEVFXEWGFWEWOWS
      WELZXAWFWSWEWSWEKUJXHVGVHXGVIVJWDVKZWEUBLZWGXIAIVLZHZVKXJXLWDXKGAIGVMXKGL
      VNGIVOVPVQVRAIVSVTXJUBKJUBWFWEWAWEUBKUJXJVGWBVJWC $.
      $( [3-Sep-2004] $) $( [24-Apr-2004] $)

    $( Cofinality is bounded by the cardinality of its argument. $)
    cflecard $p |- ( cf ` A ) C_ ( card ` A ) $=
      ( vx vy vz vw con0 wcel ccf cfv ccrd wss cv wceq wrex wral wa wex cab
      cint cfval csn ssid sseq2 rcla4ev mpan2 rgen pm3.2i fveq2 eqeq2d sseq1
      rexeq1 ralbidv anbi12d cla4egv mpan2i ss2abdv df-sn syl5ss intss syl
      fvex intsn syl6ss eqsstrd wn c0 0ss cdm wfn cffnon fndm ax-mp eleq2i
      notbii ndmfv sylbir sseq1d mpbiri pm2.61i ) AFGZAHIZAJIZKZVTWABLZCLZJIZMZ
      WEAKZDLZELZKZEWENZDAOZPZPZCQZBRZSZWBBCDEATVTWRWBUAZSZWBVTWSWQKWRWTKVTWDWB
      MZBRWQWSVTXAWPBVTXAAAKZWKEANZDAOZPZWPXBXDAUBXCDAWIAGWIWIKZXCWIUBWKXFEWIAW
      JWIWIUCUDUEUFUGWOXAXEPCAFWEAMZWGXAWNXEXGWFWBWDWEAJUHUIXGWHXBWMXDWEAAUJXGW
      LXCDAWKEWEAUKULUMUMUNUOUPBWBUQURWSWQUSUTWBAJVAVBVCVDVTVEZWCVFWBKWBVGXHWAV
      FWBXHAHVHZGZVEWAVFMXJVTXIFAHFVIXIFMVJFHVKVLVMVNAHVOVPVQVRVS $.
      $( [23-Oct-2004] $) $( [24-Apr-2004] $)

    $( Cofinality is bounded by its argument.  Exercise 1 of [TakeutiZaring]
       p. 102. $)
    cfle $p |- ( cf ` A ) C_ A $=
      ( con0 wcel ccf cfv wss ccrd cardonle wi cflecard sstr2 ax-mp syl wn c0
      0ss cdm wceq wfn cffnon fndm eleq2i notbii ndmfv sylbir sseq1d mpbiri
      pm2.61i ) ABCZADEZAFZUIAGEZAFZUKAHUJULFUMUKIAJUJULAKLMUINZUKOAFAPUNUJOAUN
      ADQZCZNUJORUPUIUOBADBSUOBRTBDUALUBUCADUDUEUFUGUH $.
      $( [23-Oct-2004] $) $( [26-Apr-2004] $)

    $( Only the ordinal zero has cofinality zero. $)
    cfeq0 $p |- ( A e. On -> ( ( cf ` A ) = (/) <-> A = (/) ) ) $=
      ( vx vy vz vw vv con0 wcel ccf cfv c0 wceq cv ccrd wss wrex wral wa wex
      cab cint cfval eqeq1d wb visset eqeq1 anbi1d exbidv elab fveq2 cardidm
      syl6eq eqeq2 mpbird adantr 19.23aiv sylbi cardon syl6eqelr ssriv onint0
      ax-mp 0ex sseq1 rexeq1 ralbidv anbi12d biimpa eqcom cvv cardeq0 bitri
      sylanb wne wn rex0 a1i rgen r19.2z mpan2 rexnal sylib necon4ai adantl
      syl syl6bi cf0 impbid1 ) AGHZAIJZKLZAKLZWIWKBMZCMZNJZLZWNAOZDMZEMOZEWNPZD
      AQZRZRZCSZBTZUAZKLZWLWIWJXFKBCDEAUBUCXGKXEHZWLXEGOXGXHUDFXEGFMZXEHZXIXINJ
      ZGXJXIWOLZXBRZCSZXKXILZXDXNBXIFUEWMXILZXCXMCXPWPXLXBWMXIWOUFUGUHUIXMXOCXL
      XOXBXLXOXKWOLXLXKWONJWOXIWONUJWNUKULXIWOXKUMUNUOUPUQXIURUSUTXEVAVBXHKWOLZ
      XBRZCSZWLXDXSBKVCWMKLZXCXRCXTWPXQXBWMKWOUFUGUHUIXRWLCXRKAOZWSEKPZDAQZRZWL
      WNKLZXBYDXQYEXBYDYEWQYAXAYCWNKAVDYEWTYBDAWSEWNKVEVFVGVHXQWOKLZYEKWOVIWNVJ
      HYFYEUDCUEWNVJVKVBVLVMYCWLYAYCAKAKVNZYBVOZDAPZYCVOYGYHDAQYIYHDAYHWRAHWSEV
      PVQVRYHDAVSVTYBDAWAWBWCWDWEUPUQUQWFWLWJKIJKAKIUJWGULWH $.
      $( [2-Feb-2005] $) $( [24-Apr-2004] $)
  $}

  ${
    $d x y z w v A $.
    $( Value of the cofinality function at a successor ordinal.  Exercise 3 of
       [TakeutiZaring] p. 102. $)
    cfsuc $p |- ( A e. On -> ( cf ` suc A ) = 1o ) $=
      ( vx vy vz vw vv con0 wcel csuc ccf cfv cv ccrd wceq wss wrex wral wa
      wex cab cint c1o sucelon cfval sylbi wn wi cardon eleq1 mpbiri adantr
      19.23aiv abssi oneqmini ax-mp csn snex fveq2 eqeq2d sseq1 rexeq1 ralbidv
      anbi12d cla4ev cardsn eqcomd wo onelss eqimss a1i jaod elsuci syl5 snidg
      jctild sseq2 rcla4ev syl6 r19.21aiv cun ssun2 df-suc sseqtr4i jctil
      sylanc 1on elisseti eqeq1 anbi1d exbidv elab sylibr c0 el1o eqcom cvv wb
      visset cardeq0 bitri rex0 nrex wne nsuceq0 r19.2z mpan mto mtbiri
      intnand imnan mpbi nex 0ex mtbir rgen sylancl eqtr4d ) AGHZAIZJKZBLZCLZMK
      ZNZYBXSOZDLZELZOZEYBPZDXSQZRZRZCSZBTZUAZUBXRXSGHXTYONAUCBCDEXSUDUEUBYNHZF
      LZYNHZUFZFUBQZUBYONZXRYNGOYPYTRUUAUGYMBGYLYAGHZCYDUUBYKYDUUBYCGHYBUHYAYCG
      UIUJUKULUMFUBYNUNUOXRUBYCNZYKRZCSZYPUBAUPZMKZNZUUFXSOZYHEUUFPZDXSQZRZUUEX
      RUUDUUHUULRCUUFAUQYBUUFNZUUCUUHYKUULUUMYCUUGUBYBUUFMURUSUUMYEUUIYJUUKYBUU
      FXSUTUUMYIUUJDXSYHEYBUUFVAVBVCVCVDXRUUGUBAGVEVFXRUUKUUIXRUUJDXSXRYFXSHZAU
      UFHZYFAOZRUUJXRUUNUUPUUOXRYFAHZYFANZVGUUPUUNXRUUQUUPUURAYFVHUURUUPUGXRYFA
      VIVJVKYFAVLVMAGVNVOYHUUPEAUUFYGAYFVPVQVRVSUUFAUUFVTXSUUFAWAAWBWCWDWEYMUUE
      BUBUBGWFWGYAUBNZYLUUDCUUSYDUUCYKYAUBYCWHWIWJWKWLYSFUBYQUBHYQWMNZYSYQWNUUT
      YRWMYNHZUVAWMYCNZYKRZCSZUVCCUVBYKUFUGUVCUFUVBYJYEUVBYBWMNZYJUFUVBYCWMNZUV
      EWMYCWOYBWPHUVFUVEWQCWRYBWPWSUOWTUVEYJYHEWMPZDXSQZUVHUVGDXSPZUVGDXSUVGUFU
      UNYHEXAVJXBXSWMXCUVHUVIAXDUVGDXSXEXFXGUVEYIUVGDXSYHEYBWMVAVBXHUEXIUVBYKXJ
      XKXLYMUVDBWMXMYAWMNZYLUVCCUVJYDUVBYKYAWMYCWHWIWJWKXNYQWMYNUIXHUEXOXPXQ $.
      $( [31-Oct-2004] $) $( [23-Apr-2004] $)
  $}

  ${
    $d x y z w $.
    $( Value of the cofinality function at omega (the set of natural numbers).
       Exercise 4 of [TakeutiZaring] p. 102. $)
    cfom $p |- ( cf ` om ) = om $=
      ( vx vy vz vw com ccf cfv cfle csn cint omex intsn cv ccrd wceq wss wrex
      wral wa wex cab eqtr cen wbr visset unbnn2 cvv wcel wb carden mp2an
      sylibr cardom syl6eq sylan2 19.23aiv ss2abi df-sn sseqtr4i intss ax-mp
      con0 omelon cfval eqsstr3i eqssi ) EFGZEEHEEIZJZVGEKLVIAMZBMZNGZOZVKEPCMD
      MPDVKQCERSZSZBTZAUAZJZVGVQVHPVIVRPVQVJEOZAUAVHVPVSAVOVSBVMVLEOVSVNVJVLEUB
      VNVLENGZEVNVKEUCUDZVLVTOZCDVKBUEZUFVKUGUHEUGUHWBWAUIWCKVKEUGUGUJUKULUMUNU
      OUPUQAEURUSVQVHUTVAEVBUHVGVROVCABCDEVDVAUSVEVF $.
      $( [27-Oct-2004] $) $( [23-Apr-2004] $)
  $}


$( I decided to comment out the Card class for now.  We have too many
   definitions, and this one doesn't really buy us much. $)

$(
  @c Card @. @( Class of all cardinal numbers @)

  @( Extend class definition to include the class of all cardinal numbers. @)
  ccdn @a class Card @.

  @( Define the class of all cardinal numbers.  The notation "Card" is used
     in Exercise 5(G) of [JustWeese] p. 174.  It should not be confused with
     the lower-case "card" for the cardinal number function ~ df-card .  @)
  df-cardn @a |- Card = ( om u. ran aleph ) @.

  @( Membership in the class of cardinal numbers. @)
  elcard @p |- ( A e. Card <-> ( card ` A ) = A ) @=
    ( ccdn wcel com cale crn cun ccrd cfv wceq df-cardn eleq2i iscard3 bitr4i )
    ABCADEFGZCAHIAJBOAKLAMN @.
    @( [31-Dec-2004] @) @( [23-Sep-2004] @)

  @( The cardinality of a set is a cardinal number. @)
  cardel @p |- ( card ` A ) e. Card @=
    ( ccrd cfv ccdn wcel wceq cardidm elcard mpbir ) ABCZDEJBCJFAGJHI @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( A natural number is a cardinal number. @)
  nncard @p |- ( A e. om -> A e. Card ) @=
    ( com wcel ccrd cfv wceq ccdn cardnn elcard sylibr ) ABCADEAFAGCAHAIJ @.
    @( [16-Jan-2005] @) @( [23-Sep-2004] @)

  @( An alternate definition of the class of all cardinal numbers. @)
  cardnum2 @p |- Card = { x | ( card ` x ) = x } @=
    ( ccdn com cale crn cun cv ccrd cfv wceq cab df-cardn wcel iscard3 bicomi
    abbi2i eqcomi eqtr4i ) BCDEFZAGZHITJZAKZLSUBUAASUATSMTNOPQR @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of cardinal numbers is a proper class.  Exercise 5(G)(b) of
     [JustWeese] p. 174. @)
  cardnprc @p |- -. Card e. _V @=
    ( vx ccdn cvv wcel cv ccrd cfv wceq cab cardprc cardnum2 eleq1i mtbir ) BCD
    AEZFGNHAIZCDAJBOCAKLM @.
    @( [25-Jan-2005] @) @( [23-Sep-2004] @)

  @( The class of transfinite cardinals (the range of the aleph function) is a
     proper class. @)
  alephprc2 @p |- -. ran aleph e. _V @=
    ( cale crn cvv wcel com cun ccdn cardnprc df-cardn eleq1i mtbi omex wa
    unexb biimpi mpan mto ) ABZCDZERFZCDZGCDUAHGTCIJKECDZSUALUBSMUAERNOPQ @.
    @( [19-Feb-2005] @) @( [23-Sep-2004] @)
$)

