$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Continuum Hypothesis equivalence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( The equivalence of two versions of the Generalized Continuum
       Hypothesis.  The right-hand side is the standard version in the
       literature.  The left-hand side is a version devised by Kannan Nambiar,
       which he calls the Axiom of Combinatorial Sets.  For the notation and
       motivation behind this axiom, see his paper, "Derivation of Continuum
       Hypothesis from Axiom of Combinatorial Sets," available at
       ~ http://www.e-atheneum.net/science/derivation&#95;ch.pdf .  The
       equivalence of the two sides provides a negative answer to Open Problem
       2 in
       ~ http://www.e-atheneum.net/science/open&#95;problem&#95;print.pdf .
       The key idea in the proof below is to equate both sides of ~ alephexp2
       to the successor aleph using ~ enen2 . $)
    gch-kn $p |- ( A e. On -> ( ( aleph ` suc A ) ~~
                { x | ( x C_ ( aleph ` A ) /\ x ~~ ( aleph ` A ) ) } <->
                ( aleph ` suc A ) ~~ ( 2o ^m ( aleph ` A ) ) ) ) $=
      ( con0 wcel csuc cale cfv c2o cmap co cen wbr cv wss wa cab cvv wb cpw
      df-pw fvex pwex eqeltrri simpl ss2abi ssexi alephexp2 enen2 sylancr
      bicomd ) BCDZBEFGZHBFGZIJZKLZULAMZUMNZUPUMKLZOZAPZKLZUKUTQDUNUTKLUOVARUTU
      QAPZUMSVBQAUMTUMBFUAUBUCUSUQAUQURUDUEUFABUGUNUTULQUHUIUJ $.
      $( [11-Jan-2006] $) $( [1-Oct-2004] $)
  $}


