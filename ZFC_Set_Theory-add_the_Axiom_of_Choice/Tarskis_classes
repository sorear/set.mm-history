$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Tarski $. $( The class of Tarski's classes. $)
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)

  $( Extend class definition to include the class of all Tarski's classes. $)
  ctsk $a class Tarski $.

  ${
    $d y z w $.
    $( The class of all Tarski's classes.  Tarski's classes is a phrase coined
       by Grzegorz Bancerek in his article "Tarski's Classes and Ranks" Journal
       of Formalized Mathematics.  Vol 1 no 3 May-August 1990.  A Tarski's
       class is a set whose existence is ensured by Tarski's axiom A (see
       ~ ax-groth and the equivalent axioms).  Axiom A was first presented in
       Tarski's article:  "&Uuml;ber unerreichbare Kardinalzahlen".  Tarski had
       invented the axiom A to enable ZFC to manage inaccessible cardinals.
       Later Grothendieck invented the concept of Grothendieck's universes and
       showed they were equal to transitive Tarski's classes. $)
    df-tsk $a |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $.
  $}

  ${
    $d w y z $.
    $( Alternate definition of the class of all Tarski's classes.  (Contributed
       by FL, 30-Dec-2010.) $)
    dftsk2 $p |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ ~P z e. y ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $=
      ( vw ctsk cv cpw wss wrex wa wral cen wbr wcel wo cab df-tsk hbra1 wi
      wceq pweq sseq1d cbvralv r19.29 visset pwex elpw ssel syl5bir imp adantl
      r19.23aiva syl ex sylbi adantr ralimdaa imdistani r19.26 3imtr4i anim1i
      simprl ssid sseq2 rcla4ev mpan2 ad2antll jca ralimiaa impbii abbii eqtri
      ) DBEZFZAEZGZVMCEZGZCVNHZIZBVNJZVLVNKLVLVNMZNBVNFJZIZAOVOVMVNMZIZBVNJZWBI
      ZAOABCPWCWGAWCWGVTWFWBVOBVNJZVRBVNJZIWHWDBVNJZIVTWFWHWIWJWHVRWDBVNVOBVNQW
      HVRWDRZWAWHVPFZVNGZCVNJZWKVOWMBCVNVLVPSVMWLVNVLVPTUAUBWNVRWDWNVRIWMVQIZCV
      NHWDWMVQCVNUCWOWDCVNWOWDVPVNMWMVQWDVQVMWLMWMWDVMVPVLBUDUEUFWLVNVMUGUHUIUJ
      UKULUMUNUOUPUQVOVRBVNURVOWDBVNURUSUTWFVTWBWEVSBVNWAWEIVOVRWAVOWDVAWDVRWAV
      OWDVMVMGZVRVMVBVQWPCVMVNVPVMVMVCVDVEVFVGVHUTVIVJVK $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    eltsk.1 $e |- T e. _V $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy cv cpw wss wrex wa wral cen wbr wcel wo ctsk wceq sseq2 rexeq
      anbi12d raleqbi1dv pweq breq2 eleq2 orbi12d raleqbidv df-tsk elab2 ) AFZG
      ZEFZHZUJBFHZBUKIZJZAUKKZUIUKLMZUIUKNZOZAUKGZKZJUJCHZUMBCIZJZACKZUICLMZUIC
      NZOZACGZKZJECPDUKCQZUPVEVAVJUOVDAUKCVKULVBUNVCUKCUJRUMBUKCSTUAVKUSVHAUTVI
      UKCUBVKUQVFURVGUKCUILUCUKCUIUDUEUFTEABUGUH $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltskg $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctsk wcel cpw wss wrex wa wral cen wbr wo wb wceq eleq1 eleq2
      sseq2 rexeq anbi12d imbi12d ralbidv2 pweq eleq2d breq2 orbi12d bibi12d
      visset eltsk vtoclg ) EFZGHZAFZIZUNJZUQBFJZBUNKZLZAUNMZUPUNNOZUPUNHZPZAUN
      IZMZLZQDGHZUQDJZUSBDKZLZADMZUPDNOZUPDHZPZADIZMZLZQEDCUNDRZUOVIVHVSUNDGSVT
      VBVMVGVRVTVAVLAUNDVTVDVOVAVLUNDUPTZVTURVJUTVKUNDUQUAUSBUNDUBUCUDUEVTVEVPA
      VFVQVTUPVFHUPVQHVEVPVTVFVQUPUNDUFUGVTVCVNVDVOUNDUPNUHWAUIUDUEUCUJABUNEUKU
      LUM $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    eltsk2.1 $e |- T e. _V $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk2 $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy ctsk wcel cv cpw wss wa wral cen wbr wo cab dftsk2 eleq2i ax-17
      wceq eleq2 sseq2 anbi12d imbi12d ralbidv2 pweq eleq2d breq2 orbi12d
      elabf bitri ) BEFBAGZHZDGZIZULUMFZJZAUMKZUKUMLMZUKUMFZNZAUMHZKZJZDOZFULBI
      ZULBFZJZABKZUKBLMZUKBFZNZABHZKZJZEVDBDAPQVCVNDBVNDRCUMBSZUQVHVBVMVOUPVGAU
      MBVOUSVJUPVGUMBUKTZVOUNVEUOVFUMBULUAUMBULTUBUCUDVOUTVKAVAVLVOUKVAFUKVLFUT
      VKVOVAVLUKUMBUEUFVOURVIUSVJUMBUKLUGVPUHUCUDUBUIUJ $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk2g $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctsk wcel cpw wss wa wral cen wbr wo eleq1 wceq sseq2 eleq2
      anbi12d raleqbi1dv pweq breq2 orbi12d raleqbidv visset eltsk2 vtoclbg )
      DEZFGAEZHZUHIZUJUHGZJZAUHKZUIUHLMZUIUHGZNZAUHHZKZJCFGUJCIZUJCGZJZACKZUICL
      MZUICGZNZACHZKZJDCBUHCFOUHCPZUNVCUSVHUMVBAUHCVIUKUTULVAUHCUJQUHCUJRSTVIUQ
      VFAURVGUHCUAVIUOVDUPVEUHCUILUBUHCUIRUCUDSAUHDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 1st axiom of a a Tarski's class.  The subsets of an element of a
       Tarski's class belong to the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tskpwss $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A C_ T ) $=
      ( vx vy wcel ctsk cpw wss cv wi wceq pweq sseq1d imbi2d wral wrex wa cen
      wbr wo eltskg simpl ralimi adantr syl6bi pm2.43i r19.21be vtoclri impcom
      ) ABEBFEZAGZBHZUJCIZGZBHZJUJULJCABUMAKZUOULUJUPUNUKBUMALMNUJUOCBUJUOCBOZU
      JUJUOUNDIHDBPZQZCBOZUMBRSUMBETCBGOZQUQCDFBUAUTUQVAUSUOCBUOURUBUCUDUEUFUGU
      HUI $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( 2nd axiom of a a Tarski's class.  The powerset of an element of a
       Tarski's class belongs to the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tskpw $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A e. T ) $=
      ( vx wcel ctsk cpw cv wi wceq pweq eleq1d imbi2d wral wss wa cen wbr wo
      eltsk2g simpr ralimi adantr syl6bi pm2.43i r19.21be vtoclri impcom ) ABDB
      EDZAFZBDZUHCGZFZBDZHUHUJHCABUKAIZUMUJUHUNULUIBUKAJKLUHUMCBUHUMCBMZUHUHULB
      NZUMOZCBMZUKBPQUKBDRCBFMZOUOCEBSURUOUSUQUMCBUPUMTUAUBUCUDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
       equipotent to the class or an element of the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tsken $p |- ( ( T e. Tarski /\ A e. ~P T ) ->
      ( A ~~ T \/ A e. T ) ) $=
      ( vx vy cpw wcel ctsk cen wbr wo cv wi wceq breq1 eleq1 orbi12d imbi2d
      wral wss wrex wa eltskg simpr syl6bi pm2.43i r19.21be vtoclri impcom ) AB
      EZFBGFZABHIZABFZJZUJCKZBHIZUNBFZJZLUJUMLCAUIUNAMZUQUMUJURUOUKUPULUNABHNUN
      ABOPQUJUQCUIUJUQCUIRZUJUJUNEZBSUTDKSDBTUACBRZUSUAUSCDGBUBVAUSUCUDUEUFUGUH
      $.
      $( [30-Dec-2010] $)
  $}

  $( The empty set is an inaccessible cardinal.  (Contributed by FL,
     30-Dec-2010.) $)
  0tsk $p |- (/) e. Tarski $=
    ( vx c0 cvv wcel ctsk 0ex cv cpw wss wa wral cen wbr wo csn pw0 eleq2i
    wceq elsni enref breq1 mpbiri orcd syl sylbi rgen a1i ral0 jctil eltsk2g
    mpbird ax-mp ) BCDZBEDZFUMUNAGZHZBIUPBDJZABKZUOBLMZUOBDZNZABHZKZJUMVCURVCUM
    VAAVBUOVBDUOBOZDZVAVBVDUOPQVEUOBRZVAUOBSVFUSUTVFUSBBLMBFTUOBBLUAUBUCUDUEUFU
    GUQAUHUIACBUJUKUL $.
    $( [30-Dec-2010] $)

  $( A element of a Tarski's class is strictly dominated by the class.  JFM
     CLASSES2 th. 1 (Contributed by FL, 22-Feb-2011.) $)
  tsksdom $p |- ( ( T e. Tarski /\ A e. T ) -> A ~< T ) $=
    ( ctsk wcel csdm wbr cpw cdom wa wi canth2g a1i wss tskpwss expcom cvv
    pwexg ssdomg syl syld com12 jcad sdomdomtr imp ) BCDZABDZABEFZUEUFAAGZEFZUH
    BHFZIUGUEUFUIUJUFUIJUEABKLUFUEUJUFUEUHBMZUJUEUFUKABNOUFUHPDUKUJJABQUHBPRSTU
    AUBAUHBCUCTUD $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2.  (Contributed by FL, 22-Feb-2011.) $)
  tskssel2 $p |- ( ( T e. Tarski /\ A e. ~P T /\ A ~< T ) -> A e. T ) $=
    ( ctsk wcel cpw csdm wbr wa cdom cen wn wi brsdom wo tsken adantl ord ex
    com23 imp sylbi com12 3impia ) BCDZABEDZABFGZABDZUFUDUEHZUGUFABIGZABJGZKZHU
    HUGLZABMUIUKULUIUHUKUGUIUHUKUGLUIUHHUJUGUHUJUGNUIABOPQRSTUAUBUC $.
    $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2.  (Contributed by FL, 17-Apr-2011.) $)
  tskssel $p |- ( ( T e. Tarski /\ A C_ T /\ A ~< T ) -> A e. T ) $=
    ( ctsk wcel wss csdm wbr cvv wi relsdom brrelexi cpw elpwg tskssel2 3exp
    com3l syl6bir com23 mpcom com13 3imp ) BCDZABEZABFGZABDZUDUCUBUEAHDZUDUCUBU
    EIZIABFJKUFUCUDUGUFUCABLDZUDUGIABHMUBUHUDUEUBUHUDUEABNOPQRSTUA $.
    $( [17-Apr-2011] $)

  $( The subsets of an element of a Tarski's class belong to the class. $)
  tskss $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( ctsk wcel wss wa cpw wb elpw2g adantl tskpwss sseld sylbird 3impia ) CDEZ
    ACEZBAFZBCEZPQGZRBAHZEZSQUBRIPBACJKTUACBACLMNO $.
    $( [18-Jun-2013] $)

  $( A singleton of an element of a Tarski's class belongs to the class.  JFM
     CLASSES2 th. 2 (partly) (Contributed by FL, 22-Feb-2011.) $)
  tsksn $p |- ( ( T e. Tarski /\ A e. T ) -> { A } e. T ) $=
    ( ctsk wcel cpw csn tskpw wss snsspw tskss mp3an3 syldan ) BCDZABDAEZBDZAFZ
    BDZABGMOPNHQAINPBJKL $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  ${
    $d T x $.
    $( A non empty Tarski's class contains the empty set.  (Contributed by FL,
       30-Dec-2010.) $)
    tsk0 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> (/) e. T ) $=
      ( vx c0 wne ctsk wcel cv wex wi n0 wss 0ss tskss mp3an3 expcom 19.23aiv
      sylbi impcom ) ACDZAEFZCAFZSBGZAFZBHTUAIZBAJUCUDBTUCUATUCCUBKUAUBLUBCAMNO
      PQR $.
      $( [18-Jun-2013] $) $( [30-Dec-2010] $)
  $}

  $( One is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.) $)
  tsk1 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 1o e. T ) $=
    ( ctsk wcel c0 wne wa c1o csn df1o2 tsk0 tsksn syldan syl5eqel ) ABCZADEZFG
    DHZAINODACPACAJDAKLM $.
    $( [22-Feb-2011] $)

  $( Two is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.) $)
  tsk2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o e. T ) $=
    ( ctsk wcel c0 wne wa c2o csn cpr df2o2 c1o tsk1 wi df1o2 eleq1i cpw pwpw0
    tskpw syl5eqelr ex adantr com12 sylbi mpcom syl5eqel ) ABCZADEZFZGDDHZIZAJK
    ACZUHUJACZALUKUIACZUHULMKUIANOUHUMULUFUMULMUGUFUMULUFUMFUJUIPAQUIARSTUAUBUC
    UDUE $.
    $( [22-Feb-2011] $)

  $( If a Tarski's class is not empty it has more than two elements.
     (Contributed by FL, 22-Feb-2011.) $)
  2domtsk $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o ~< T ) $=
    ( ctsk wcel c0 wne c2o csdm wbr tsk2 tsksdom syldan ) ABCADEFACFAGHAIFAJK
    $.
    $( [22-Feb-2011] $)

  ${
    $d T x y $.
    $( A nonempty Tarski's class is infinite, because it contains all the
       finite levels of the cumulative hierarchy.  (This proof does not use
       ~ ax-inf .) $)
    tskr1om $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cv cfv wceq wrex wi wfun con0
      wfn r1fnon fnfun ax-mp fvelima mpan csuc fveq2 eleq1d r10 tsk0 syl5eqel
      cpw tskpw nnon r1suc syl syl5ibr exp3a adantrd finds2 eleq1 imbi2d
      syl5ibcom r19.23aiv com12 ssrdv ) ADEZAFGZHZBIJKZABLZVIEZVHVJAEZVKCLZIMZV
      JNZCJOZVHVLPZIQZVKVPIRSVRTRIUAUBCVJJIUCUDVOVQCJVMJEVHVNAEZPVOVQVSFIMZAEVJ
      IMZAEZVJUEZIMZAEZVHCBVMFNVNVTAVMFIUFUGVMVJNVNWAAVMVJIUFUGVMWCNVNWDAVMWCIU
      FUGVHVTFAUHAUIUJVJJEZVFWBWEPVGWFVFWBWEVFWBHWEWFWAUKZAEWAAULWFWDWGAWFVJREW
      DWGNVJUMVJUNUOUGUPUQURUSVOVSVLVHVNVJAUTVAVBVCUOVDVE $.
      $( [24-Jun-2013] $)

    $( A nonempty Tarski's class contains the whole finite cumulative
       hierarchy.  (This proof does not use ~ ax-inf .) $)
    tskr1om2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> U. ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cuni cv wrex eluni2 wss wi cfv
      wceq wtr wfun con0 wfn r1fnon fnfun ax-mp fvelima mpan r1tr treq mpbii
      r19.23aivw trss 3syl adantl tskr1om sseld tskss 3exp adantr syld imp
      r19.23adva syl5bi ssrdv ) ADEZAFGZHZBIJKZLZABMZVJEVKCMZEZCVINVHVKAEZCVKVI
      OVHVMVNCVIVHVLVIEZHVMVKVLPZVNVOVMVPQZVHVOVKIRZVLSZBJNZVLTZVQIUAZVOVTIUBUC
      WBUDUBIUEUFBVLJIUGUHVSWABJVSVRTWAVKUIVRVLUJUKULVLVKUMUNUOVHVOVPVNQZVHVOVL
      AEZWCVHVIAVLAUPUQVFWDWCQVGVFWDVPVNVLVKAURUSUTVAVBVAVCVDVE $.
      $( [22-Feb-2011] $)
  $}

  $( A nonempty Tarski's class is infinite. $)
  tskinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
    ( ctsk wcel c0 wne wa com cr1 cima cen wbr cdom omex con0 cvv wf1 wss r111
    omsson f1imaen mp2an ensymi tskr1om wi relen brrelexi ssdomg mp2b syl
    endomtr sylancr ) ABCADEFZGHGIZJKUMALKZGALKUMGMNOHPGNQUMGJKZRSNOGHMTUAZUBUL
    UMAQZUNAUCUOUMOCUQUNUDUPUMGJUEUFUMAOUGUHUIGUMAUJUK $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their unordered pair
     is also an element of the class.  JFM CLASSES2 th. 3 (partly).
     (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by Mario
     Carneiro, 20-Jun-2013.) $)
  tskpr $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> { A , B } e. T ) $=
    ( ctsk wcel w3a cpr wss csdm wbr simp1 wa prssg ibi 3adant1 com cdom c0
    wne ne0i tskinf sylan2 wi cfn prfi isfinite3 mpbi sdomdomtr mpani adantr
    mpd 3adant3 tskssel syl111anc ) CDEZACEZBCEZFUOABGZCHZURCIJZURCEUOUPUQKUPUQ
    USUOUPUQLUSABCCCMNOUOUPUTUQUOUPLPCQJZUTUPUOCRSVACATCUAUBUOVAUTUCUPUOURPIJZV
    AUTURUDEVBABUEURUFUGURPCDUHUIUJUKULURCUMUN $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their ordered pair is
     also an element of the class.  JFM CLASSES2 th. 4.  (Contributed by FL,
     22-Feb-2011.) $)
  tskop $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> <. A , B >. e. T ) $=
    ( ctsk wcel w3a cop csn cpr df-op simp1 tsksn 3adant3 tskpr syl111anc
    syl5eqel ) CDEZACEZBCEZFZABGAHZABIZIZCABJTQUACEZUBCEUCCEQRSKQRUDSACLMABCNUA
    UBCNOP $.
    $( [22-Feb-2011] $)

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A cross product of two parts of a Tarski's class is a part of the
       class.  (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by
       Mario Carneiro, 20-Jun-2013.) $)
    tskxpss $p |- ( ( T e. Tarski /\ A C_ T /\ B C_ T )
    -> ( A X. B ) C_ T ) $=
      ( vz vx vy ctsk wcel wss cxp wa xpss12 wi cv cop wceq wrex elxp2 w3a
      tskop eleq1a syl 3expib r19.23advv syl5bi ssrdv sstr expcom syl5 3impib
      ) CGHZACIZBCIZABJZCIZULUMKUNCCJZIZUKUOACBCLUKUPCIZUQUOMUKDUPCDNZUPHUSENZF
      NZOZPZFCQECQUKUSCHZEFUSCCRUKVCVDEFCCUKUTCHZVACHZVCVDMZUKVEVFSVBCHVGUTVACT
      VBCUSUAUBUCUDUEUFUQURUOUNUPCUGUHUBUIUJ $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d T x y $.
    $( A Tarski's class is well-orderable.  (Contributed by Mario Carneiro,
       20-Jun-2013.) $)
    tskwe2 $p |- ( T e. Tarski -> E. x e. On x ~~ T ) $=
      ( vy ctsk wcel cv csdm wbr cpw crab wss cen con0 wrex wi wral tskssel2
      3exp r19.21aiv rabss sylibr tskwe syl ) BDEZCFZBGHZCBIZJBKZAFBLHAMNUDUFUE
      BEZOZCUGPUHUDUJCUGUDUEUGEUFUIUEBQRSUFCUGBTUACABUBUC $.
      $( [20-Jun-2013] $)
  $}

  ${
    $d A x y z $.  $d T x y z $.
    unifo.1 $e |- T = U_ x e. A ( { x } X. x ) $.
    $( Existence of an onto function for a union. $)
    unifo $p |- ( 2nd |` T ) : T -onto-> U. A $=
      ( vy vz cuni c2nd cres wfo wfn crn wceq df-fo cvv wss wf fo2nd fof ffn
      mp2b ssv fnssres mp2an cima df-ima cv cfv wrex wcel csn cxp ciun eleq2i
      eliun bitri xp2nd eleq1 syl5ib reximdv syl5bi impcom r19.23aiva wa cop
      visset snid biantrur opelxp bitr4i sneq xpeq12 mpancom eleq2d rcla4ev
      sylan2b cbviunv eqtri sylibr op2nd fveq2 eqeq1d sylancl impbii wb
      fvelimab eluni2 3bitr4i eqriv eqtr3i mpbir2an ) CBGZHCIZJWMCKZWMLZWLMCWLW
      MNHOKZCOPZWNOOHJOOHQWPROOHSOOHTUAZCUBZOCHUCUDHCUEZWOWLHCUFEWTWLFUGZHUHZEU
      GZMZFCUIZXCAUGZUJZABUIZXCWTUJZXCWLUJXEXHXDXHFCXDXACUJZXHXJXAXFUKZXFULZUJZ
      ABUIZXDXHXJXAABXLUMZUJXNCXOXADUNAXABXLUOUPXDXMXGABXMXBXFUJXDXGXAXKXFUQXBX
      CXFURUSUTVAVBVCXGXEABXFBUJZXGVDZXFXCVEZCUJZXRHUHZXCMZXEXQXRXAUKZXAULZUJZF
      BUIZXSXGXPXRXLUJZYEXGXFXKUJZXGVDYFYGXGXFAVFZVGVHXFXCXKXFEVFZVIVJYDYFFXFBX
      AXFMZYCXLXRYBXKMYJYCXLMXAXFVKYBXKXAXFVLVMVNVOVPXSXRFBYCUMZUJYECYKXRCXOYKD
      AFBXLYCXKYBMXFXAMXLYCMXFXAVKXKYBXFXAVLVMVQVRUNFXRBYCUOUPVSXFXCYHYIVTXDYAF
      XRCXAXRMXBXTXCXAXRHWAWBVOWCVCWDWPWQXIXEWEWRWSFOCXCHWFUDAXCBWGWHWIWJWK $.
      $( [24-Jun-2013] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is equipotent
       to ` A ` . $)
    inar1 $p |- ( A e. Inacc -> ( R1 ` A ) ~~ A ) $=
      ( vx vy vz vw cina wcel cr1 cfv cdom wbr cen cxp cv ciun con0 wlim wceq
      cwina inawina winaon syl winalim r1lim syl11anc wral wa csdm onelon
      sylan wi c0 csuc eleq1 fveq2 breq1d imbi12d wne ne0i 0sdomg syl5ibr r10
      breq1i syl6ibr 3syl wtr word eloni ordtr trsuc ex adantl ccrd cpw r1suc
      fvex cardid ensymi pwen ax-mp syl6eqbr wb winacard eleq2d cvv cardsdom
      sylancr bitr3d ccf elina simp3bi pweq rcla4cv sylbird imp ensdomtr
      syl2an expr imim12d cun visset mpan iunex iundom ax-17 hbiu1 hbbr wss
      ssiun2 ssdomg mpsyl endomtr vtoclgaf mprg ssun2 mp2 unex xpdom2 ssun1
      xpdom1 domtr mp2an com limomss ssexg sylancl syl6ss sylc infxpidm
      domentr eqbrtrd ad2antlr wn wo eleq1a ordirr nsyli ad2ant2r simpll wf
      wrex wex ccom cres wfn limord elon sylibr cardf r1fnon dffn2 mpbi fco
      onss fssres ffn simpr simplll ontr1 syl12anc biimpd imim2d mpid fvres
      wfun ffun fvco3 mp3an12 eqtrd eleq1d sylibrd ralimdva impr ffnfv
      sylanbrc eleq2 biimpa eliun cardon onelssi sseq2d reximdva syl5bi syl5
      expdimp r19.21aiv resfunexg feq1 fveq1 rexbidv ralbidv anbi12d cla4ev
      ee12an ad2antrl cfflb syld simp2bi sseq1d sylibd ontri1 mt2d ioran iunon
      a1i eqcom ordequn mtod onelss sylsyld iunss unss biimpi cif id iuneq1
      uneq12d 0elon elimel elisseti onun2i dedth adantr onsseleq mpbid orcomd
      ord mpd iscard simprbi breq1 domsdomtr exp43 com4l tfinds2 imp3a mpcom
      sdomdom r19.21aiva breq2 raleqbi1dv xpeq12 anidms breq12d vtoclg winainf
      infxpen onssr1 sbth ) AFGZAHIZAJKZAVUKJKZVUKALKVUJVUKAAMZJKVUNALKZVULVUJV
      UKBABNZHIZOZVUNJVUJAPGZAQZVUKVURRVUJASGZVUSATZAUAZUBZVUJVVAVUTVVBAUCUBBAP
      UDUEVUJVUSVUQAJKZBAUFZVURVUNJKZVVDVUJVVEBAVUJVUPAGZUGZVUQAUHKZVVEVUPPGZVV
      IVVJVUJVUSVVHVVKVVDAVUPUIUJVVKVUJVVHVVJVVHVVJUKULAGZULHIZAUHKZUKZCNZAGZVV
      PHIZAUHKZUKZVVPUMZAGZVWAHIZAUHKZUKZVUJBCVUPULRZVVHVVLVVJVVNVUPULAUNVWFVUQ
      VVMAUHVUPULHUOUPUQVUPVVPRZVVHVVQVVJVVSVUPVVPAUNVWGVUQVVRAUHVUPVVPHUOUPUQV
      UPVWARZVVHVWBVVJVWDVUPVWAAUNVWHVUQVWCAUHVUPVWAHUOUPUQVUJVVAVUSVVOVVBVVCVU
      SVVLULAUHKZVVNVVLVWIVUSAULURZAULUSAPUTVAVVMULAUHVBVCVDVEVVPPGZVUJVVTVWEUK
      VWKVUJUGVWBVVQVVSVWDVUJVWBVVQUKZVWKVUJVUSAVFZVWLVVDVUSAVGZVWMAVHZAVIUBVWM
      VWBVVQAVVPVJVKVEVLVWKVUJVVSVWDVWKVWCVVRVMIZVNZLKVWQAUHKZVWDVUJVVSUGVWKVWC
      VVRVNZVWQLVVPVOVVRVWPLKZVWSVWQLKVWPVVRVVPHVPZVVRVQVRZVVRVWPVVRVMVPZVSVTWA
      VUJVVSVWRVUJVVSVWPAGZVWRVUJVVAVXDVVSWBVVBVVAVWPAVMIZGZVXDVVSVVAVXEAVWPAWC
      ZWDVVAVVRWEGZVUSVXFVVSWBZVXAVVCVVRAWEPWFZWGWHUBVUJDNZVNZAUHKZDAUFZVXDVWRU
      KVUJVWJAWIIZARZVXNDAWJZWKVXMVWRDVWPAVXKVWPRVXLVWQAUHVXKVWPWLUPWMUBWNWOVWC
      VWQAWPWQWRWSVKVVHVUPQZVUJVVTCVUPUFZVVJVVHVXRVUJVXSVVJVVHVXRUGZVUJVXSUGZUG
      ZVUQVUPCVUPVWPOZWTZJKZVYDAUHKZVVJVXRVYEVVHVYAVXRVUQDVUPVXKHIZOZVYDJVUPWEG
      ZVXRVUQVYHRBXAZDVUPWEUDXBVXRVYHVUPVYCMZJKZVYKVYDJKZVYHVYDJKVYGVYCJKZVYLDV
      UPDVUPVYCVYGVYJCVUPVWPVYJVXCXCZVXKHVPXDVVRVYCJKZVYNCBVXKVUPVUPVXKGCXECEVY
      GVYCJENZVYGGCXEVYQJGCXECEVUPVWPXFXGVVPVXKRVVRVYGVYCJVVPVXKHUOUPVVPVUPGZVW
      TVWPVYCJKZVYPVXBVWPWEGVYRVWPVYCXHVYSVXCCVUPVWPXIVWPVYCWEXJXKVVRVWPVYCXLWG
      XMXNVXRVYKVYDVYDMZJKZVYTVYDLKZVYMVYKVUPVYDMZJKZWUCVYTJKZWUAVYCVYDJKZWUDVY
      CWEGVYCVYDXHWUFVYOVYCVUPXOVYCVYDWEXJXPVYCVYDVUPVUPVYCVYJVYOXQZVYJXRVTVUPV
      YDJKZWUEVYIVUPVYDXHWUHVYJVUPVYCXSZVUPVYDWEXJXPVUPVYDVYDWUGWUGXTVTVYKWUCVY
      TYAYBVXRYCVYDJKZWUBVXRYCWEGZYCVYDXHWUJVXRYCVUPXHVYIWUKVUPYDZVYJYCVUPWEYEY
      FVXRYCVUPVYDWULWUIYGYCVYDWEXJYHVYDWUGYIUBVYKVYTVYDYJWGVYHVYKVYDYAWGYKYLVY
      BVYDAGZVYFVYBVYDARZYMWUMVYBWUNAVUPRZAVYCRZYNZVYBWUOYMZWUPYMWUQYMVVHVUJWUR
      VXRVXSVVHVUJWURVVHWUOAAGZVUJVUPAAYOVUJVUSVWNWUSYMVVDVWOAYPVEYQWOYRVYBWUPV
      VHVVHVXRVYAYSZVYBWUPAVUPXHZVVHYMZVYBWUPVXOVUPXHZWVAVYBWUPVUPAVYQYTZVXKVVP
      VYQIZXHZCVUPUUAZDAUFZUGZEUUBZWVCVYBVUPAVMHUUCZVUPUUDZYTZWUPVXKVVPWVLIZXHZ
      CVUPUUAZDAUFZWVJVYBWVLVUPUUEZWVNAGZCVUPUFZWVMVYBVVKVUPPWVLYTZWVRVXRVVKVVH
      VYAVXRVUPVGZVVKVUPUUFVUPVYJUUGUUHZYLZVVKPPWVKYTZVUPPXHWWAWEPVMYTZPWEHYTZW
      WEUUIHPUUEWWGUUJPHUUKUULZPWEPVMHUUMYBZVUPUUNPPVUPWVKUUOWGVUPPWVLUUPVEVXTV
      UJVXSWVTVXTVUJUGZVVTWVSCVUPWWJVYRUGZVVTVXDWVSWWKVVTVVQVXDWWKVUSVYRVVHVVQV
      UJVUSVXTVYRVVDYLZWWJVYRUUQVVHVXRVUJVYRUURVUSVYRVVHUGVVQVVPVUPAUUSWOUUTWWK
      VVSVXDVVQWWKVVSVXDWWKVXFVVSVXDWWKVXHVUSVXIVXAWWLVXJWGWWKVXEAVWPVUJVXEARZV
      XTVYRVUJVVAWWMVVBVXGUBZYLWDWHUVAUVBUVCZWWKWVNVWPAWWJVVKVYRWVNVWPRVXRVVKVV
      HVUJWWCYLVVKVYRUGZWVNVVPWVKIZVWPVYRWVNWWQRVVKVVPVUPWVKUVDVLWWPVWKWWQVWPRZ
      VUPVVPUIVMUVEZWWGVWKWWRWWFWWSUUIWEPVMUVFVTWWHPWEVVPVMHUVGUVHUBUVIZUJUVJUV
      KUVLUVMCVUPAWVLUVNUVOVYBVVKWUPWVQUKWWDVVKWUPWVQVVKWUPUGWVPDAVVKWUPVXKAGZW
      VPWUPWXAUGVXKVYCGZVVKWVPWUPWXAWXBAVYCVXKUVPUVQWXBVXKVWPGZCVUPUUAVVKWVPCVX
      KVUPVWPUVRVVKWXCWVOCVUPWXCWVOWWPVXKVWPXHVWPVXKVVRUVSZUVTWWPWVNVWPVXKWWTUW
      AVAUWBUWCUWDUWEUWFVKUBWVIWVMWVQUGEWVLWVKUVEZVYIWVLWEGWWEWXEWWIPPWVKUVFVTV
      YJWVKVUPWEUWGYBVYQWVLRZWVDWVMWVHWVQVUPAVYQWVLUWHWXFWVGWVPDAWXFWVFWVOCVUPW
      XFWVEWVNVXKVVPVYQWVLUWIUWAUWJUWKUWLUWMUWNVYBVUSVVKWVJWVCUKVUJVUSVXTVXSVVD
      UWOZWWDDCAVUPEUWPUEUWQVUJWVCWVAWBVXTVXSVUJVXOAVUPVUJVWJVXPVXNVXQUWRUWSUWO
      UWTVYBVUSVVKWVAWVBWBWXGWWDAVUPUXAUEUWTUXBWUOWUPUXCUVOVYBVVKVYCPGZWUNWUQUK
      WWDVWPPGZWXHCVUPCVUPVWPVYJVXCUXDWXIVYRWXDUXEXNWUNAVYDRZVVKWXHUGWUQVYDAUXF
      VVKWWBVYCVGWXJWUQUKWXHVUPVHVYCVHAVUPVYCUXGWQUWCYFUXHVYBWUNWUMVYBWUMWUNVYB
      VYDAXHZWUMWUNYNZVYBVUPAXHZVYCAXHZWXKVYBVUSVVHWXMWXGWUTAVUPUXIYHVYBVWPAXHZ
      CVUPUFZWXNVXTVUJVXSWXPWWJVVTWXOCVUPWWKVUSVVTVXDWXOWWLWWOAVWPUXIUXJUVLUVMC
      VUPVWPAUXKUUHWXMWXNUGWXKVUPVYCAUXLUXMUEVXTVYDPGZVUSWXKWXLWBVYAVXRWXQVVHVX
      RVVKWXQWWCVVKWXQVVKVUPULUXNZCWXRVWPOZWTZPGVUPULVUPWXRRZVYDWXTPWYAVUPWXRVY
      CWXSWYAUXOCVUPWXRVWPUXPUXQUVJWXRWXSVUPULPUXRUXSZWXIWXSPGCWXRCWXRVWPWXRPWY
      BUXTVXCUXDWXIVVPWXRGWXDUXEXNUYAUYBUBVLVUJVUSVXSVVDUYCVYDAUYDWQUYEUYFUYGUY
      HVYBWWMVXKAUHKZDAUFZWUMVYFUKVUJWWMVXTVXSWWNUWOWWMVUSWYDDAUYIUYJWYCVYFDVYD
      AVXKVYDAUHUYKWMVEUYHVUQVYDAUYLUEUYMUYNUYOUYPUYQVUQAUYRUBUYSVUQVXKJKZBVXKU
      FZBVXKVUQOZVXKVXKMZJKZUKVVFVVGUKDAPVXKARZWYFVVFWYIVVGWYEVVEBVXKAVXKAVUQJU
      YTVUAWYJWYGVURWYHVUNJBVXKAVUQUXPWYJWYHVUNRVXKAVXKAVUBVUCVUDUQBVXKVXKVUQDX
      AZWYKVUPHVPXDVUEYHYKVUJVUSYCAXHZVUOVVDVUJVVAWYLVVBAVUFUBAVUGUEVUKVUNAYJUE
      VUJVUSAVUKXHZVUMVVDVUJVVAVUSWYMVVBVVCAVUHVEAVUKPXJYHVUKAVUIUE $.
      $( [6-Jun-2013] $)
  $}

  ${
    $( The set of hereditarily finite sets is countable.  TODO:  Prove without
       AC and Infinity. $)
    r1om $p |- ( R1 ` om ) ~~ om $=
      ( com cina wcel cr1 cfv cen wbr omina inar1 ax-mp ) ABCADEAFGHAIJ $.
      $( [27-May-2013] $)
  $}

  ${
    $d A y z $.  $d B y z $.  $d C w $.  $d D y $.  $d w x y $.  $d w z y $.
    abrexco.1 $e |- B e. _V $.
    abrexco.2 $e |- ( y = B -> C = D ) $.
    $( Composition of two image maps ` C ( y ) ` and ` B ( w ) ` . $)
    abrexco $p |- { x | E. y e. { z | E. w e. A z = B } x = C } =
        { x | E. w e. A x = D } $=
      ( cv wceq wrex cab wa wex wcel df-rex visset eqeq1 rexbidv elab anbi1i
      r19.41v bitr4i exbii bitri rexcom4 19.41v eqeq2d pm5.32i isseti biantrur
      3bitr4i rexbii 3bitr2i abbii ) AKZGLZBCKZFLZDEMZCNZMZURHLZDEMZAVDBKZFLZUS
      OZDEMZBPZVIBPZDEMVFVDVGVCQZUSOZBPVKUSBVCRVNVJBVNVHDEMZUSOVJVMVOUSVBVOCVGB
      SUTVGLVAVHDEUTVGFTUAUBUCVHUSDEUDUEUFUGVIDBEUHVLVEDEVHVEOZBPVHBPZVEOVLVEVH
      VEBUIVIVPBVHUSVEVHGHURJUJUKUFVQVEBFIULUMUNUOUPUQ $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( Any set must be at least as large as the cofinality of its rank, because
       the ranks of the elements of ` A ` form a cofinal map into
       ` ( rank `` A ) ` . $)
    rankcf $p |- -. A ~< ( cf ` ( rank ` A ) ) $=
      ( vx vy vw vz crnk cfv c0 wceq cv csuc con0 wrex cvv wcel wlim wa w3o
      ccf csdm wbr wn rankon onzsl mpbi cdom 0dom fveq2 cf0 syl6eq breq1d
      mpbiri domnsym syl c1o cfsuc sylan9eqr wne nsuceq0 neeq1 0elon rankonid
      necon3i cr1 cima cuni wb rankvaln necon1ai breq2 bibi12d visset
      0sdom1dom 0sdom bitr3i vtoclg mpbird adantl eqbrtrd r19.23aiva csn cab
      wi nlim0 limeq mtbiri con4i fveq2d breq12 mpdan rexeq abbidv mpancom
      imbi12d cmpt crn eqid rnmpt cen cfon sdomdom rcla4ev sylancr ondomen wfo
      wfn fnmpt fvex a1i mprg dffn4 fodomnum mpi 3syl syl5eqbrr wss id r1elssi
      ssel2 sylan ranksnb rankelb limsuc sylibd imp eqeltrd eleq1a r19.23adva
      abssdv ciun snex dfiun2 iunid eqtr3i fveq2i snwf abrexexg eleq1 sseq1
      r1elss rankuni2b syl5eqr abrexco unieqi eqtri syl6req cfslb syl111anc
      domtr ex syld pm2.01d 3jaoi ax-mp ) AFGZHIZUUOBJZKZIZBLMZUUONOZUUOPZQZRZA
      UUOSGZTUAZUBZUUOLOUVDAUCBUUOUDUEUUPUVGUUTUVCUUPUVEAUFUAZUVGUUPUVHHAUFUAAU
      GUUPUVEHAUFUUPUVEHSGHUUOHSUHUIUJUKULUVEAUMZUNUUTUVHUVGUUSUVHBLUUQLOZUUSQU
      VEUOAUFUUSUVJUVEUURSGUOUUOUURSUHUUQUPUQUUSUOAUFUAZUVJUUSUUOHURZUVKUUSUVLU
      URHURUUQUSUUOUURHUTULUVLUVKAHURZAHUUOHAHIUUOHFGZHAHFUHHLOUVNHIVAHVBUEUJVC
      UVLAVDLVEVFZOZUVKUVMVGZUVPUUOHAVHZVIUOCJZUFUAZUVSHURZVGUVQCAUVOUVSAIZUVTU
      VKUWAUVMUVSAUOUFVJUVSAHUTVKUVTHUVSTUAUWAUVSCVLZVMUVSUWCVNVOVPUNVQUNVRVSVT
      UVIUNUVBUVGUVAUVBUVFUVBUVFDJZUUQWAZFGZIZBAMZDWBZAUFUAZUVGUVBUVPUVFUWJWCZU
      VPUVBUVPUBUUPUVBUBUVRUUPUVBHPWDUUOHWEWFUNWGZUVSUVSFGZSGZTUAZUWGBUVSMZDWBZ
      UVSUFUAZWCUWKCAUVOUWBUWOUVFUWRUWJUWBUWNUVEIUWOUVFVGUWBUWMUUOSUVSAFUHWHUVS
      AUWNUVETWIWJUWQUWIIUWBUWRUWJVGUWBUWPUWHDUWGBUVSAWKWLUWQUWIUVSAUFWIWMWNUWO
      UWQBUVSUWFWOZWPZUVSUFBDUVSUWFUWSUWSWQZWRUWOUVSUUQUFUAZBLMZUUQUVSWSUABLMZU
      WTUVSUFUAZUWOUWNLOUVSUWNUFUAZUXCUWMWTUVSUWNXAUXBUXFBUWNLUUQUWNUVSUFVJXBXC
      BBUVSXDUXDUVSUWTUWSXEZUXEUWSUVSXFZUXGUWFNOZUXHBUVSBUVSUWFUWSNUXAXGUXIUUQU
      VSOUWEFXHXIXJUVSUWSXKUEBUVSUWTUWSUWCXLXMXNXOVPUNUVBUVEUWIUFUAZUWJUVGWCUVB
      UVBUWIUUOXPUWIVFZUUOIZUXJUVBXQUVBUWHDUUOUVBUWGUWDUUOOZBAUVBUUQAOZQZUWFUUO
      OUWGUXMWCUXOUWFUUQFGZKZUUOUXOUUQUVOOZUWFUXQIUVBAUVOXPZUXNUXRUVBUVPUXSUWLA
      XRZUNAUVOUUQXSZXTUUQYAUNUVBUXNUXQUUOOZUVBUXNUXPUUOOZUYBUVBUVPUXNUYCWCUWLU
      UQAYBUNUUOUXPYCYDYEYFUWFUUOUWDYGUNYHYIUVBUVPUXLUWLUVPUUOEUVSUWEIZBAMZCWBZ
      EJZFGZYJZUXKUVPUUOUYFVFZFGZUYIUYJAFBAUWEYJUYJABCAUWEUUQYKZYLBAYMYNYOUVPUY
      FUVOOZUYKUYIIUVPUYMUYFUVOXPZUVPUYECUVOUVPUYDUVSUVOOZBAUVPUXNQZUWEUVOOZUYD
      UYOWCUYPUXRUYQUVPUXSUXNUXRUXTUYAXTUUQYPUNUWEUVOUVSYGUNYHYIUVPUYFNOUYMUYNV
      GZBCAUWEUVOYQUYGUVOOZUYGUVOXPZVGUYREUYFNUYGUYFIUYSUYMUYTUYNUYGUYFUVOYRUYG
      UYFUVOYSVKUYGEVLYTVPUNVQEUYFUUAUNUUBUYIUWDUYHIEUYFMDWBZVFUXKEDUYFUYHUYGFX
      HYLVUAUWIDECBAUWEUYHUWFUYLUYGUWEFUHUUCUUDUUEUUFUNUUOUWIAFXHUUGUUHUXJUWJUV
      GUXJUWJQUVHUVGUVEUWIAUUIUVIUNUUJUNUUKUULVRUUMUUN $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is a Tarski's
       class. $)
    inatsk $p |- ( A e. Inacc -> ( R1 ` A ) e. Tarski ) $=
      ( vx vy cina wcel cv cpw cr1 cfv wss wa wral cen wbr wo ctsk cwina wi
      inawina wrex ciun con0 wlim wceq winaon winalim r1lim syl11anc eleq2d
      eliun syl6bb csuc wb onelon sylan r1pw syl limsuc r1ord2 sylbid imp
      sseld r19.23adva cuni elssuni r1tr2 syl6ss jca r19.21aiva crnk r1suc
      rankr1ai syl6bir fvex elsuc sylib orcomd cdom csdm wn elpwi ssdomg mpd
      ad2antlr ccf rankcf fveq2 c0 wne w3a elina simp2 sylbi sylan9eqr breq2d
      mtbii inar1 sdomentr exp3acom23 sylc adantr mtod adantlr bren2 sylanbrc
      ex cima r1elwf rankr1ag biimprd orim12d eltsk2 ) ADEZBFZGZAHIZJZXOXPEZKZB
      XPLXNXPMNZXNXPEZOZBXPGZLXPPEXMXSBXPXMYAKZXQXRYDXRXQXMYAXRXMAQEZYAXRRASZYE
      YAXNCFZHIZEZCATZXRYEYAXNCAYHUAZEYJYEXPYKXNYEAUBEZAUCZXPYKUDAUEZAUFZCAUBUG
      UHUICXNAYHUJUKYEYIXRCAYEYGAEZKZYIXOYGULZHIZEZXRYQYGUBEZYIYTUMYEYLYPUUAYNA
      YGUNUOXNYGUPUQYQYSXPXOYEYPYSXPJZYEYPYRAEZUUBYEYMYPUUCUMYOAYGURUQYEYLUUCUU
      BRYNYRAUSUQUTVAVBUTVCUTUQVAZXRXOXPVDXPXOXPVEAVFVGUQUUDVHVIXMYBBYCXMXNYCEZ
      KZXNVJIZAUDZUUGAEZOYBUUFUUIUUHUUFUUGAULZEZUUIUUHOXMUUEUUKXMUUEXNUUJHIZEZU
      UKXMYLUUMUUEUMXMYEYLYFYNUQZYLUULYCXNAVKUIUQZXNUUJVLVMVAUUGAXNVJVNVOVPVQUU
      FUUHXTUUIYAUUFUUHXTUUFUUHKXNXPVRNZXNXPVSNZVTZXTUUEUUPXMUUHUUEXNXPJUUPXNXP
      WAXNXPYCWBWCWDXMUUHUURUUEXMUUHKZUUQXNAVSNZUUSXNUUGWEIZVSNUUTXNWFUUSUVAAXN
      VSUUHXMUVAAWEIZAUUGAWEWGXMAWHWIZUVBAUDZXOAVSNBALZWJUVDBAWKUVCUVDUVEWLWMWN
      WOWPXMUUQUUTRZUUHXMYLXPAMNZUVFUUNAWQYLUUQUVGUUTXNXPAUBWRWSWTXAXBXCXNXPXDX
      EXFUUFYAUUIUUFXNHUBXGVDEZYLYAUUIUMXMUUEUVHXMUUEUUMUVHUUOXNUUJXHVMVAXMYLUU
      EUUNXAXNAXIUHXJXKWCVIBXPAHVNXLXE $.
      $( [8-Jun-2013] $)
  $}

  $( The set of hereditarily finite sets is a Tarski's class.  (The
     Tarski-Grothendieck Axiom is not needed for this theorem.) $)
  r1omtsk $p |- ( R1 ` om ) e. Tarski $=
    ( com cina wcel cr1 cfv ctsk omina inatsk ax-mp ) ABCADEFCGAHI $.
    $( [28-May-2013] $)

  ${
    $d T x y $.  $d A x $.
    $( A Tarski's class contains all ordinals smaller than it. $)
    tskord $p |- ( ( T e. Tarski /\ A e. On /\ A ~< T ) -> A e. T ) $=
      ( vx vy con0 wcel ctsk csdm wbr cv wa wi wceq breq1 anbi2d eleq1 imbi12d
      wral simplrl cdom cvv wss visset onelss imp ssdomg mpsyl adantlr simplrr
      domsdomtr syl11anc pm2.27 ralimdva dfss3 tskssel 3exp syl5bir com23
      adantl syld ex tfis2 vtoclga 3impib 3com12 ) AEFZBGFZABHIZABFZVFVGVHVIVGC
      JZBHIZKZVJBFZLZVGVHKZVILCAEVJAMZVLVOVMVIVPVKVHVGVJABHNOVJABPQVNVGDJZBHIZK
      ZVQBFZLZCDVJVQMZVLVSVMVTWBVKVRVGVJVQBHNOVJVQBPQVJEFZVLWADVJRZVMWCVLWDVMLW
      CVLKZWDVTDVJRZVMWEWAVTDVJWEVQVJFZKZVGVRWAVTLWCVGVKWGSWHVQVJTIZVKVRWCWGWIV
      LVQUAFWCWGKVQVJUBZWIDUCWCWGWJVJVQUDUEVQVJUAUFUGUHWCVGVKWGUIVQVJBUJUKVSVTU
      LUKUMVLWFVMLZWCVGVKWKVGWFVKVMWFVJBUBZVGVKVMLDVJBUNVGWLVKVMVJBUOUPUQURUEUS
      UTVAURVBVCVDVE $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d T w x y z $.
    $( An even more direct relationship than ~ r1tskina to get an inacessible
       cardinal out of a Tarski's class: the size of any nonempty Tarski's
       class is an inaccessible cardinal. $)
    tskcard $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( card ` T ) e. Inacc ) $=
      ( vx vz vy ctsk wcel c0 wne wa ccrd cfv ccf wceq cv cpw csdm wbr wral
      w3a cina cardeq0 necon3bid biimpar wn cmap co cale wss con0 crab cint
      cdom cmpt eqid pwcfsdom com visset pwex canth2 cen cvv simpl cardon
      onelon mpan adantl cardsdomelir tskord syl111anc tskpw tskpwss syldan
      ssdomg mpsyl wrex wi tskssel2 3exp r19.21aiv rabss sylibr tskwe syl
      cardid2 ensymg mpd adantr domentr syl11anc sdomdomtr ax-mp sylancr
      r19.21aiva inawinalem winainflem mp3an2 sylan2 cardidm cardaleph sylancl
      fveq2d opreq12d breq12d mpbiri oprex simp1 simp3 cxp wf fvex elmap fssxp
      sylbi ex ssrdv cfle sstr tskxpss com23 mpdi sstr2 syl2im sylc simp2 wfn
      ffn fndmeng ensym ensdomtr syl2an tskssel 3expia domnsym mt2d wo wb cfon
      onsseleq mp2an mpbi ori 3jca elina ) AEFZAGHZIZAJKZGHZUUGLKZUUGMZBNZOZUUG
      PQZBUUGRZSUUGTFUUFUUHUUJUUNUUDUUHUUEUUDUUGGAGAEUAUBUCZUUFUUIUUGFZUDUUJUUF
      UUPUUGUUGUUIUEUFZPQZUUFUURUUGUUKUGKUHBUIUJUKZUGKZUUTUUTLKZUEUFZPQCUUSDCUV
      AUUKCNDNZKULQBUIUJUMZBUVDUNUOUUFUUGUUTUUQUVBPUUFUPUUGUHZUUGJKUUGMUUGUUTMU
      UFUUHUUNUVEUUOUUDUUNUUEUUDUUMBUUGUUDUUKUUGFZIZUULUULOZPQZUVHUUGULQZUUMUUL
      UUKBUQZURZUSUVGUVHAULQZAUUGUTQZUVJUVHVAFUVGUVHAUHZUVMUULUVLURUUDUVFUUKAFZ
      UVOUVGUUDUUKUIFZUUKAPQZUVPUUDUVFVBUVFUVQUUDUUGUIFZUVFUVQAVCZUUGUUKVDVEVFU
      VFUVRUUDUUKAVGVFUUKAVHVIZUUDUVPUULAFUVOUUKAVJUULAVKVLVLUVHAVAVMVNUUDUVNUV
      FUUDUUGAUTQZUVNUUDUVCAUTQDUIVOZUWBUUDUVRBAOZUJAUHZUWCUUDUVRUVPVPZBUWDRUWE
      UUDUWFBUWDUUDUUKUWDFUVRUVPUUKAVQVRVSUVRBUWDAVTWABDAWBWCDAWDWCUUGAEWEWFZWG
      UVHAUUGWHWIUVSUVIUVJIUUMVPUVTUULUVHUUGUIWJWKWLWMWGZUUNUUHUUKUVCPQDUUGVOBU
      UGRZUVEUVSUUNUWIVPUVTBDUUGWNWKUUHUVSUWIUVEUVTBDUUGWOWPWQWIAWRBUUGWSWTZUUF
      UUGUUTUUIUVAUEUWJUUFUUGUUTLUWJXAXBXCXDUUDUUPUURUDZVPUUEUUDUUPUWKUUDUUPIZU
      UQUUGULQZUWKUWLUUQAULQZUVNUWMUUQVAFUWLUUQAUHUWNUUGUUIUEXEUWLBUUQAUUDUUPUU
      KUUQFZUVPUUDUUPUWOSZUUDUUKAUHZUVRUVPUUDUUPUWOXFZUWPUWOUUDUWQUUDUUPUWOXGZU
      WRUWOUUKUUIUUGXHZUHZUUDUWTAUHZUWQUWOUUIUUGUUKXIZUXAUUGUUIUUKAJXJUUGLXJZXK
      ZUUIUUGUUKXLXMUUDUUGAUHZUXBUUDBUUGAUUDUVFUVPUWAXNXOUUDUXFUUIAUHZUXBUUIUUG
      UHZUXFUXGUUGXPZUUIUUGAXQVEUUDUXGUXFUXBUUDUXGUXFUXBUUIUUGAXRVRXSXTWFUUKUWT
      AYAYBYCUWPUWOUUPUVRUWSUUDUUPUWOYDUWOUUKUUIUTQZUUIAPQUVRUUPUWOUUIUUKUTQZUX
      JUWOUXCUXKUXEUXCUUKUUIYEUUIVAFUXKUUIUUGUUKYFUXDUUIVAUUKYGWTXMUUIUUKUVKYHW
      CUUIAVGUUKUUIAYIYJWIUUKAYKVIYLXOUUQAVAVMVNUUDUVNUUPUWGWGUUQAUUGWHWIUUQUUG
      YMWCXNWGYNUUPUUJUXHUUPUUJYOZUXIUUIUIFUVSUXHUXLYPUUGYQUVTUUIUUGYRYSYTUUAWC
      UWHUUBBUUGUUCWA $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d A x $.
    $( There is a direct relationship between transitive Tarski's classes and
       inacessible cardinals: the Tarski's classes that occur in the cumulative
       hierarchy are exactly at the strongly inaccessible cardinals. $)
    r1tskina $p |- ( A e. On ->
        ( ( R1 ` A ) e. Tarski <-> ( A = (/) \/ A e. Inacc ) ) ) $=
      ( vx con0 wcel cr1 cfv ctsk c0 wceq cina wo wa wn wne df-ne ccrd cvv cen
      wbr fvex simplr simpll crnk csuc cima cuni onwf sseli eqid rankr1c mpbii
      syl simpld rankonid biimpi fveq2d eleq2d mtbid adantl cpw wss onssr1
      elpwg mpbird tsken sylan2 ord mt3d syl11anc carden2b mpsyl cv csdm wral
      simpl adantr sseld imp cdom visset canth2 pwex tskpwss ssdomg wi
      sdomdomtr ax-mp sylancr ensym sdomentr syl12anc r19.21aiva iscard
      sylanbrc eqtr3d r10 on0eln0 biimpar r1sdom syldan syl5eqbrr 0sdom sylib
      adantlr tskcard eqeltrrd ex syl5bir orrd 0tsk fveq2 syl6eq eleq1d mpbiri
      inatsk jaoi a1i impbid ) ACDZAEFZGDZAHIZAJDZKZXSYAYDXSYALZYBYCYBMAHNZYEYC
      AHOYEYFYCYEYFLZXTPFZAJYGAPFZYHAXTQDZYGAXTRSZYIYHIAETZYGYAXSYKXSYAYFUAZXSY
      AYFUBYAXSLZYKAXTDZXSYOMYAXSAAUCFZEFZDZYOXSYRMZAYPUDEFDZXSAECUEUFZDZYSYTLZ
      CUUAAUGUHUUBYPYPIUUCYPUIAYPUJUKULUMXSYQXTAXSYPAEXSYPAIAUNUOUPUQURUSYNYKYO
      XSYAAXTUTDZYKYOKXSUUDAXTVAZAVBZAXTCVCVDAXTVEVFVGVHZVIAXTQVJVKYEYIAIZYFYEX
      SBVLZAVMSZBAVNUUHXSYAVOYEUUJBAYEUUIADZLZXSUUIXTVMSZXTARSZUUJXSYAUUKUBZUUL
      YAUUIXTDZUUMXSYAUUKUAZYEUUKUUPYEAXTUUIXSUUEYAUUFVPVQVRYAUUPLZUUIUUIUTZVMS
      ZUUSXTVSSZUUMUUIBVTZWAUUSQDUURUUSXTVAUVAUUIUVBWBUUIXTWCUUSXTQWDVKYJUUTUVA
      LUUMWEYLUUIUUSXTQWFWGWHVIUULYAXSUUNUUQUUOYNYKUUNUUGAXTYLWIULVIXSUUMUUNLUU
      JUUIXTACWJVRWKWLBAWMWNVPWOYGYAXTHNZYHJDYMXSYFUVCYAXSYFLZHXTVMSUVCUVDHHEFZ
      XTVMWPXSYFHADZUVEXTVMSXSUVFYFAWQWRAHWSWTXAXTYLXBXCXDXTXEVIXFXGXHXIXGYDYAW
      EXSYBYAYCYBYAHGDXJYBXTHGYBXTUVEHAHEXKWPXLXMXNAXOXPXQXR $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F y $.
    $( A version of ~ fodom that doesn't require the Axiom of Choice ~ ax-ac .
       This version uses a sethood predicate as an antecedent.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    fodomnumg $p |- ( ( A e. V /\ E. x e. On x ~~ A
        /\ F : A -onto-> B ) -> B ~<_ A ) $=
      ( vy wcel cv cen wbr con0 wrex wfo cdom wa wi wceq breq2 rexbidv foeq2
      anbi12d imbi12d visset fodomnum imp vtoclg 3impib ) BEGAHZBIJZAKLZBCDMZCB
      NJZUHFHZIJZAKLZUMCDMZOZCUMNJZPUJUKOZULPFBEUMBQZUQUSURULUTUOUJUPUKUTUNUIAK
      UMBUHIRSUMBCDTUAUMBCNRUBUOUPURAUMCDFUCUDUEUFUG $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A f x y z $.  $d T f x y z $.
    $( The union of an element of a transitive Tarski's class is in the set.
       (Contributed by Mario Carneiro, 22-Jun-2013.) $)
    tskuni $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> U. A e. T ) $=
      ( vf vz vx vy ctsk wcel wtr w3a cuni cen wbr wn ccrd cfv cv wf1o wex wa
      cima wceq wrex cab wne ccf csdm tsksdom con0 tskwe2 cardid2 syl ensymg
      mpd adantr wi cardon sdomentr ax-mp syl11anc cdom sdomdom breq2 rcla4ev
      sylancr ondomen cmpt crn eqid rnmpt wfo wfn cvv fnmpt visset imaexg a1i
      mprg dffn4 mpbi fodomnumg mp3an3 syl5eqbrr sylan2 domsdomtr sylan
      anabss3 adantll mpdan cina c0 ne0i tskcard cpw wral elina simp2 sylbi
      breqtrrd 3adant2 wlim wss cwina inawina winalim 3syl eqeq1 rexbidv elab
      imassrn f1ofo forn syl5sseq ad2antlr wf1 f1of1 elssuni f1imaen syl2an
      trss imp 3adant1 ssel2 simpl1 fvex adantlr ensdomtr jca sseq1 breq1
      anbi12d biimprcd r19.23adva syl5bi r19.21aiv cfslb2n ciun dfiun2 a1d
      iunss sylibr foelrn ex wf fof eluni2 ax-17 hbiu1 hbel wfun cdm fdm
      sseq2d syl5ibr ffun jctild funfvima2 syl6 com23 ssiun2 sseld sylcom
      r19.23ad eleq1a r19.23adv syld ssrdv eqssd syl5eqr necon3ai pm2.01d
      nexdv entr bren sylib expcom 3ad2ant1 mtod wo uniss df-tr biimpi sstr
      elpw2g tsken sylbird syl5 3impib ord ) BGHZBIZABHZJZAKZBLMZNUWNBHZUWMUWOU
      WNBOPZCQZRZCSZUWMUWSCUWMUWSUWMUWSUWSNZUWMUWSTZDQZUWREQZUAZUBZEAUCZDUDZKZU
      WQUEZUXAUXBUXHUWQUFPZUGMZUXJUWMUXLUWSUWJUWLUXLUWKUWJUWLTZUXHUWQUXKUGUXMAU
      WQUGMZUXHUWQUGMZUXMABUGMZBUWQLMZUXNABUHUWJUXQUWLUWJUWQBLMZUXQUWJUXDBLMEUI
      UCUXREBUJEBUKULUWQBGUMUNZUOUWQUIHZUXPUXQTUXNUPBUQZABUWQUIURUSUTUWLUXNUXOU
      WJUWLUXNUXOUWLUXNTUXHAVAMZUXNUXOUXNUWLUXDALMEUIUCZUYBUXNAUXDVAMZEUIUCZUYC
      UXNUXTAUWQVAMZUYEUYAAUWQVBUYDUYFEUWQUIUXDUWQAVAVCVDVEEEAVFULUWLUYCTUXHEAU
      XEVGZVHZAVAEDAUXEUYGUYGVIZVJUWLUYCAUYHUYGVKZUYHAVAMUYGAVLZUYJUXEVMHZUYKEA
      EAUXEUYGVMUYIVNUYLUXDAHZUWRVMHUYLCVOUWRUXDVMVPUSZVQVRAUYGVSVTEAUYHUYGBWAW
      BWCWDUXHAUWQWEWFWGWHWIUXMUWQWJHZUXKUWQUBZUWLUWJBWKUEUYOBAWLBWMWDZUYOUWQWK
      UEZUYPUXDWNUWQUGMEUWQWOZJUYPEUWQWPUYRUYPUYSWQWRZULWSWTUOUXBUWQXAZFQZUWQXB
      ZVUBUXKUGMZTZFUXHWOUXLUXJUPUXBUYOUWQXCHVUAUWMUYOUWSUWJUWLUYOUWKUYQWTUOZUW
      QXDUWQXEXFUXBVUEFUXHVUBUXHHVUBUXEUBZEAUCZUXBVUEUXGVUHDVUBFVOUXCVUBUBUXFVU
      GEAUXCVUBUXEXGXHXIUXBVUGVUEEAUXBUYMTZUXEUWQXBZUXEUXKUGMZTZVUGVUEUPVUIVUJV
      UKUWSVUJUWMUYMUWSUWRVHZUXEUWQUWRUXDXJUWSUWNUWQUWRVKZVUMUWQUBUWNUWQUWRXKZU
      WNUWQUWRXLULXMZXNVUIUXEUWQUXKUGVUIUXEUXDLMZUXDUWQUGMZUXEUWQUGMUWSUYMVUQUW
      MUWSUWNUWQUWRXOUXDUWNXBZVUQUYMUWNUWQUWRXPUXDAXQZUWNUWQUXDUWREVOXRXSWHUWMU
      YMVURUWSUWMUYMTZUXDBUGMZUXQVURVVAUXDBHZVVBUWMABXBZUYMVVCUWKUWLVVDUWJUWKUW
      LVVDBAXTZYAYBABUXDYCWFVVAUWJVVCVVBUWJUWKUWLUYMYDZUXDBUHWFWIVVAUWJUXQVVFUX
      SULUWQVMHVVBUXQTVURUPBOYEZUXDBUWQVMURUSUTYFUXEUXDUWQYGUTUXBUYPUYMUXBUYOUY
      PVUFUYTULUOWSYHVUGVUEVULVUGVUCVUJVUDVUKVUBUXEUWQYIVUBUXEUXKUGYJYKYLULYMYN
      YOFUWQUXHVVGYPUTUNUWSUXIUWQUWSUXIEAUXEYQZUWQEDAUXEUYNYRUWSVVHUWQUWSVUJEAW
      OVVHUWQXBUWSVUJEAUWSVUJUYMVUPYSYOEAUXEUWQYTUUAUWSFUWQVVHUWSVUNVUBUWQHZVUB
      VVHHZUPVUOVUNVVIVUBUXCUWRPZUBZDUWNUCZVVJVUNVVIVVMDUWNUWQVUBUWRUUBUUCVUNUW
      NUWQUWRUUDZVVMVVJUPUWNUWQUWRUUEVVNVVLVVJDUWNVVNUXCUWNHZVVKVVHHZVVLVVJUPVV
      OUXCUXDHZEAUCVVNVVPEUXCAUUFVVNVVQVVPEAVVNEUUGEFFVVKVVHVUBVVKHEUUGEFAUXEUU
      HUUIVVNVVQUYMVVPVVNVVQUYMVVPUPVVNVVQTUYMVVKUXEHZVVPVVNVVQUYMVVRUPVVNUYMVV
      QVVRVVNUYMUWRUUJZUXDUWRUUKZXBZTVVQVVRUPVVNUYMVWAVVSUYMVWAVVNVUSVUTVVNVVTU
      WNUXDUWNUWQUWRUULUUMUUNUWNUWQUWRUUOUUPUXDUXCUWRUUQUURUUSYAUYMUXEVVHVVKEAU
      XEUUTUVAUVBUUCUUSUVCYNVVKVVHVUBUVDUURUVEULUVFULUVGUVHUVIUVJULUUCUVKUVLUWJ
      UWKUWOUWTUPUWLUWOUWJUWTUWOUWJTUWNUWQLMZUWTUWJUWOUXQVWBUXSUWNBUWQUVMWDUWNU
      WQCVVGUVNUVOUVPUVQUVRUWMUWOUWPUWJUWKUWLUWOUWPUVSZUWKUWLTUWNBXBZUWJVWCUWKU
      WLVWDUWKUWLVVDVWDVVEVVDUWKVWDVVDUWNBKZXBVWEBXBZVWDUWKABUVTUWKVWFBUWAUWBUW
      NVWEBUWCXSUVPUVFYAUWJVWDUWNBWNHZVWCUWNBGUWDUWJVWGVWCUWNBUWEUUCUWFUWGUWHUW
      IUN $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A transitive Tarski's class is closed under small unions.  (Contributed
       by Mario Carneiro, 22-Jun-2013.) $)
    tskurn $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ F : A --> T )
    -> U. ran F e. T ) $=
      ( vx vy ctsk wcel wtr wa wf w3a crn cuni simp1l simp1r wss csdm wbr frn
      3ad2ant3 cdom cv cen con0 wrex wfo simp2 wwe wex trss sylc tskwe2 ween
      mpbird wess eximdv syl5com wb syl mpbid wfn ffn dffn4 sylib fodomnumg
      syl111anc tsksdom syl11anc domsdomtr tskssel tskuni ) BFGZBHZIZABGZABCJZK
      ZVLVMCLZBGZVRMBGVLVMVOVPNZVLVMVOVPOZVQVLVRBPZVRBQRZVSVTVPVNWBVOABCSTVQVRA
      UARZABQRZWCVQVODUBZAUCRDUDUEZAVRCUFZWDVNVOVPUGZVQAEUBZUHZEUIZWGVQVLABPZWL
      VTVQVMVOWMWAWIBAUJUKVLBWJUHZEUIZWMWLVLWOWFBUCRDUDUEDBULDBFEUMUNWMWNWKEABW
      JUOUPUQUKVQVOWLWGURWIDABEUMUSUTVPVNWHVOVPCAVAWHABCVBACVCVDTDAVRCBVEVFVQVL
      VOWEVTWIABVGVHVRABVIVHVRBVJVFVRBVKVF $.
      $( [22-Jun-2013] $)
  $}

  $( Two ways of saying a class of ordinals is unbounded.  (Contributed by
     Mario Carneiro, 8-Jun-2013.) $)
  ssonprc $p |- ( A C_ On -> ( A e/ _V <-> U. A = On ) ) $=
    ( cvv wnel wcel wn con0 wss cuni wceq df-nel word wo ssorduni ordeleqon
    sylib orcomd ord elisset uniexb sylibr syl6 con1d wi onprc uniexg eleq1
    syl5ib mtoi a1i impbid syl5bb ) ABCABDZEZAFGZAHZFIZABJUNUMUPUNUPULUNUPEUOFD
    ZULUNUPUQUNUQUPUNUOKUQUPLAMUONOPQUQUOBDZULUOFRASTUAUBUPUMUCUNUPULFBDZUDULUR
    UPUSABUEUOFBUFUGUHUIUJUK $.
    $( [8-Jun-2013] $)


