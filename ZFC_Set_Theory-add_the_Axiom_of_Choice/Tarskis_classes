$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Tarski's classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Tarski $. $( The class of Tarski's classes. $)
  $( A function whose value is the smallest Tarski's classe
     containing a particular set. $)

  $( Extend class definition to include the class of all Tarski's classes. $)
  ctsk $a class Tarski $.

  ${
    $d y z w $.
    $( The class of all Tarski's classes.  Tarski's classes is a phrase coined
       by Grzegorz Bancerek in his article "Tarski's Classes and Ranks" Journal
       of Formalized Mathematics.  Vol 1 no 3 May-August 1990.  A Tarski's
       class is a set whose existence is ensured by Tarski's axiom A (see
       ~ ax-groth and the equivalent axioms).  Axiom A was first presented in
       Tarski's article:  "&Uuml;ber unerreichbare Kardinalzahlen".  Tarski had
       invented the axiom A to enable ZFC to manage inaccessible cardinals.
       Later Grothendieck invented the concept of Grothendieck's universes and
       showed they were equal to transitive Tarski's classes. $)
    df-tsk $a |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ E. w e. y ~P z C_ w ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $.
  $}

  ${
    $d w y z $.
    $( Alternate definition of the class of all Tarski's classes.  (Contributed
       by FL, 30-Dec-2010.) $)
    dftsk2 $p |- Tarski = { y | ( A. z e. y ( ~P z C_ y
      /\ ~P z e. y ) /\ A. z e. ~P y ( z ~~ y \/ z e. y ) ) } $=
      ( vw ctsk cv cpw wss wrex wa wral cen wbr wcel wo cab df-tsk hbra1 r19.26
      wi anim1i wceq pweq sseq1d cbvralv r19.29 visset pwex elpw syl5bir adantl
      ssel imp rexlimiva ex sylbi adantr ralimdaa imdistani 3imtr4i simprl ssid
      syl sseq2 rcla4ev mpan2 ad2antll jca ralimiaa impbii abbii eqtri ) DBEZFZ
      AEZGZVMCEZGZCVNHZIZBVNJZVLVNKLVLVNMZNBVNFJZIZAOVOVMVNMZIZBVNJZWBIZAOABCPW
      CWGAWCWGVTWFWBVOBVNJZVRBVNJZIWHWDBVNJZIVTWFWHWIWJWHVRWDBVNVOBVNQWHVRWDSZW
      AWHVPFZVNGZCVNJZWKVOWMBCVNVLVPUAVMWLVNVLVPUBUCUDWNVRWDWNVRIWMVQIZCVNHWDWM
      VQCVNUEWOWDCVNWOWDVPVNMWMVQWDVQVMWLMWMWDVMVPVLBUFUGUHWLVNVMUKUIULUJUMVBUN
      UOUPUQURVOVRBVNRVOWDBVNRUSTWFVTWBWEVSBVNWAWEIVOVRWAVOWDUTWDVRWAVOWDVMVMGZ
      VRVMVAVQWPCVMVNVPVMVMVCVDVEVFVGVHTVIVJVK $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    eltsk.1 $e |- T e. _V $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy cv cpw wss wrex wa wral cen wbr wcel ctsk wceq sseq2 rexeq anbi12d
      wo raleqbi1dv pweq breq2 eleq2 orbi12d raleqbidv df-tsk elab2 ) AFZGZEFZH
      ZUJBFHZBUKIZJZAUKKZUIUKLMZUIUKNZTZAUKGZKZJUJCHZUMBCIZJZACKZUICLMZUICNZTZA
      CGZKZJECODUKCPZUPVEVAVJUOVDAUKCVKULVBUNVCUKCUJQUMBUKCRSUAVKUSVHAUTVIUKCUB
      VKUQVFURVGUKCUILUCUKCUIUDUEUFSEABUGUH $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T w y z $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltskg $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ E. w e. T ~P z C_ w ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctsk wcel cpw wss wrex wa wral cen wo wb anbi12d imbi12d ralbidv2
      wbr wceq eleq1 eleq2 sseq2 rexeq pweq eleq2d breq2 orbi12d bibi12d visset
      eltsk vtoclg ) EFZGHZAFZIZUNJZUQBFJZBUNKZLZAUNMZUPUNNTZUPUNHZOZAUNIZMZLZP
      DGHZUQDJZUSBDKZLZADMZUPDNTZUPDHZOZADIZMZLZPEDCUNDUAZUOVIVHVSUNDGUBVTVBVMV
      GVRVTVAVLAUNDVTVDVOVAVLUNDUPUCZVTURVJUTVKUNDUQUDUSBUNDUEQRSVTVEVPAVFVQVTU
      PVFHUPVQHVEVPVTVFVQUPUNDUFUGVTVCVNVDVOUNDUPNUHWAUIRSQUJABUNEUKULUM $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    eltsk2.1 $e |- T e. _V $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk2 $p |- ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) $=
      ( vy ctsk wcel cv cpw wss wa wral cen wbr wo dftsk2 eleq2 anbi12d imbi12d
      cab ralbidv2 eleq2i ax-17 wceq sseq2 eleq2d breq2 orbi12d elabf bitri
      pweq ) BEFBAGZHZDGZIZULUMFZJZAUMKZUKUMLMZUKUMFZNZAUMHZKZJZDSZFULBIZULBFZJ
      ZABKZUKBLMZUKBFZNZABHZKZJZEVDBDAOUAVCVNDBVNDUBCUMBUCZUQVHVBVMVOUPVGAUMBVO
      USVJUPVGUMBUKPZVOUNVEUOVFUMBULUDUMBULPQRTVOUTVKAVAVLVOUKVAFUKVLFUTVKVOVAV
      LUKUMBUJUEVOURVIUSVJUMBUKLUFVPUGRTQUHUI $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d T y z $.
    $( Properties of a Tarski's class.  (Contributed by FL, 30-Dec-2010.) $)
    eltsk2g $p |- ( T e. A -> ( T e. Tarski <-> ( A. z e. T ( ~P z C_ T
      /\ ~P z e. T ) /\ A. z e. ~P T ( z ~~ T \/ z e. T ) ) ) ) $=
      ( vy cv ctsk wcel cpw wss wa wral cen wbr wo eleq1 sseq2 eleq2 raleqbi1dv
      wceq anbi12d pweq breq2 orbi12d raleqbidv visset eltsk2 vtoclbg ) DEZFGAE
      ZHZUHIZUJUHGZJZAUHKZUIUHLMZUIUHGZNZAUHHZKZJCFGUJCIZUJCGZJZACKZUICLMZUICGZ
      NZACHZKZJDCBUHCFOUHCSZUNVCUSVHUMVBAUHCVIUKUTULVAUHCUJPUHCUJQTRVIUQVFAURVG
      UHCUAVIUOVDUPVEUHCUILUBUHCUIQUCUDTAUHDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 1st axiom of a a Tarski's class.  The subsets of an element of a
       Tarski's class belong to the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tskpwss $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A C_ T ) $=
      ( vx vy wcel ctsk cpw wss cv wi wceq pweq sseq1d imbi2d wral wrex cen wbr
      wa wo eltskg simpl ralimi adantr syl6bi pm2.43i r19.21be vtoclri impcom )
      ABEBFEZAGZBHZUJCIZGZBHZJUJULJCABUMAKZUOULUJUPUNUKBUMALMNUJUOCBUJUOCBOZUJU
      JUOUNDIHDBPZSZCBOZUMBQRUMBETCBGOZSUQCDFBUAUTUQVAUSUOCBUOURUBUCUDUEUFUGUHU
      I $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x $.
    $( 2nd axiom of a a Tarski's class.  The powerset of an element of a
       Tarski's class belongs to the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tskpw $p |- ( ( T e. Tarski /\ A e. T ) -> ~P A e. T ) $=
      ( vx wcel ctsk cpw cv wi wceq pweq eleq1d imbi2d wral wss cen wbr eltsk2g
      wa wo simpr ralimi adantr syl6bi pm2.43i r19.21be vtoclri impcom ) ABDBED
      ZAFZBDZUHCGZFZBDZHUHUJHCABUKAIZUMUJUHUNULUIBUKAJKLUHUMCBUHUMCBMZUHUHULBNZ
      UMRZCBMZUKBOPUKBDSCBFMZRUOCEBQURUOUSUQUMCBUPUMTUAUBUCUDUEUFUG $.
      $( [30-Dec-2010] $)
  $}

  ${
    $d A x $.  $d T x y $.
    $( 3rd axiom of a Tarski's class.  A subset of a Tarski's class is either
       equipotent to the class or an element of the class.  (Contributed by FL,
       30-Dec-2010.) $)
    tsken $p |- ( ( T e. Tarski /\ A e. ~P T ) ->
      ( A ~~ T \/ A e. T ) ) $=
      ( vx vy cpw wcel ctsk cen wbr wo cv wi wceq breq1 orbi12d imbi2d wral wss
      eleq1 wa wrex eltskg simpr syl6bi pm2.43i r19.21be vtoclri impcom ) ABEZF
      BGFZABHIZABFZJZUJCKZBHIZUNBFZJZLUJUMLCAUIUNAMZUQUMUJURUOUKUPULUNABHNUNABS
      OPUJUQCUIUJUQCUIQZUJUJUNEZBRUTDKRDBUATCBQZUSTUSCDGBUBVAUSUCUDUEUFUGUH $.
      $( [30-Dec-2010] $)
  $}

  $( The empty set is an inaccessible cardinal.  (Contributed by FL,
     30-Dec-2010.) $)
  0tsk $p |- (/) e. Tarski $=
    ( vx c0 cvv wcel ctsk 0ex cv cpw wss wa wral cen wbr wo csn pw0 eleq2i wceq
    elsni enref breq1 mpbiri orcd syl sylbi rgen a1i jctil eltsk2g mpbird ax-mp
    ral0 ) BCDZBEDZFUMUNAGZHZBIUPBDJZABKZUOBLMZUOBDZNZABHZKZJUMVCURVCUMVAAVBUOV
    BDUOBOZDZVAVBVDUOPQVEUOBRZVAUOBSVFUSUTVFUSBBLMBFTUOBBLUAUBUCUDUEUFUGUQAULUH
    ACBUIUJUK $.
    $( [30-Dec-2010] $)

  $( A element of a Tarski's class is strictly dominated by the class.  JFM
     CLASSES2 th. 1 (Contributed by FL, 22-Feb-2011.) $)
  tsksdom $p |- ( ( T e. Tarski /\ A e. T ) -> A ~< T ) $=
    ( ctsk wcel csdm wbr cpw cdom wa wi canth2g a1i tskpwss expcom pwexg ssdomg
    wss cvv syl syld com12 jcad sdomdomtr imp ) BCDZABDZABEFZUEUFAAGZEFZUHBHFZI
    UGUEUFUIUJUFUIJUEABKLUFUEUJUFUEUHBQZUJUEUFUKABMNUFUHRDUKUJJABOUHBRPSTUAUBAU
    HBCUCTUD $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2.  (Contributed by FL, 22-Feb-2011.) $)
  tskssel2 $p |- ( ( T e. Tarski /\ A e. ~P T /\ A ~< T ) -> A e. T ) $=
    ( ctsk wcel cpw csdm wbr wa cdom cen wn wi brsdom wo tsken adantl ord com23
    ex imp sylbi com12 3impia ) BCDZABEDZABFGZABDZUFUDUEHZUGUFABIGZABJGZKZHUHUG
    LZABMUIUKULUIUHUKUGUIUHUKUGLUIUHHUJUGUHUJUGNUIABOPQSRTUAUBUC $.
    $( [22-Feb-2011] $)

  $( A part of a Tarski's class strictly dominated by the class is an element
     of the class.  JFM CLASSES2 th. 2.  (Contributed by FL, 17-Apr-2011.) $)
  tskssel $p |- ( ( T e. Tarski /\ A C_ T /\ A ~< T ) -> A e. T ) $=
    ( ctsk wcel wss csdm wbr cvv relsdom brrelexi cpw elpwg tskssel2 3exp com3l
    wi syl6bir com23 mpcom com13 3imp ) BCDZABEZABFGZABDZUDUCUBUEAHDZUDUCUBUEPZ
    PABFIJUFUCUDUGUFUCABKDZUDUGPABHLUBUHUDUEUBUHUDUEABMNOQRSTUA $.
    $( [17-Apr-2011] $)

  $( The subsets of an element of a Tarski's class belong to the class. $)
  tskss $p |- ( ( T e. Tarski /\ A e. T /\ B C_ A ) -> B e. T ) $=
    ( ctsk wcel wss wa cpw wb elpw2g adantl tskpwss sseld sylbird 3impia ) CDEZ
    ACEZBAFZBCEZPQGZRBAHZEZSQUBRIPBACJKTUACBACLMNO $.
    $( [18-Jun-2013] $)

  $( A singleton of an element of a Tarski's class belongs to the class.  JFM
     CLASSES2 th. 2 (partly) (Contributed by FL, 22-Feb-2011.) $)
  tsksn $p |- ( ( T e. Tarski /\ A e. T ) -> { A } e. T ) $=
    ( ctsk wcel cpw csn tskpw wss snsspw tskss mp3an3 syldan ) BCDZABDAEZBDZAFZ
    BDZABGMOPNHQAINPBJKL $.
    $( [18-Jun-2013] $) $( [22-Feb-2011] $)

  ${
    $d T x $.
    $( A non empty Tarski's class contains the empty set.  (Contributed by FL,
       30-Dec-2010.) $)
    tsk0 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> (/) e. T ) $=
      ( vx c0 wne ctsk wcel cv wex wi wss 0ss tskss mp3an3 expcom exlimiv sylbi
      n0 impcom ) ACDZAEFZCAFZSBGZAFZBHTUAIZBAQUCUDBTUCUATUCCUBJUAUBKUBCALMNOPR
      $.
      $( [18-Jun-2013] $) $( [30-Dec-2010] $)
  $}

  $( One is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.) $)
  tsk1 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 1o e. T ) $=
    ( ctsk wcel c0 wne wa c1o csn df1o2 tsk0 tsksn syldan syl5eqel ) ABCZADEZFG
    DHZAINODACPACAJDAKLM $.
    $( [22-Feb-2011] $)

  $( Two is an element of a non empty Tarski's class.  (Contributed by FL,
     22-Feb-2011.) $)
  tsk2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o e. T ) $=
    ( ctsk wcel c0 wne wa c2o csn cpr df2o2 c1o wi df1o2 eleq1i cpw pwpw0 tskpw
    tsk1 syl5eqelr ex adantr com12 sylbi mpcom syl5eqel ) ABCZADEZFZGDDHZIZAJKA
    CZUHUJACZARUKUIACZUHULLKUIAMNUHUMULUFUMULLUGUFUMULUFUMFUJUIOAPUIAQSTUAUBUCU
    DUE $.
    $( [22-Feb-2011] $)

  $( If a Tarski's class is not empty it has more than two elements.
     (Contributed by FL, 22-Feb-2011.) $)
  2domtsk $p |- ( ( T e. Tarski /\ T =/= (/) ) -> 2o ~< T ) $=
    ( ctsk wcel c0 wne c2o csdm wbr tsk2 tsksdom syldan ) ABCADEFACFAGHAIFAJK
    $.
    $( [22-Feb-2011] $)

  ${
    $d T x y $.
    $( A nonempty Tarski's class is infinite, because it contains all the
       finite levels of the cumulative hierarchy.  (This proof does not use
       ~ ax-inf .) $)
    tskr1om $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cv cfv wceq wrex wi fveq2 eleq1d
      con0 syl wfun wfn r1fnon fnfun ax-mp fvelima mpan csuc r10 syl5eqel tskpw
      tsk0 cpw nnon r1suc syl5ibr exp3a adantrd finds2 eleq1 syl5ibcom rexlimiv
      imbi2d com12 ssrdv ) ADEZAFGZHZBIJKZABLZVIEZVHVJAEZVKCLZIMZVJNZCJOZVHVLPZ
      IUAZVKVPISUBVRUCSIUDUECVJJIUFUGVOVQCJVMJEVHVNAEZPVOVQVSFIMZAEVJIMZAEZVJUH
      ZIMZAEZVHCBVMFNVNVTAVMFIQRVMVJNVNWAAVMVJIQRVMWCNVNWDAVMWCIQRVHVTFAUIAULUJ
      VJJEZVFWBWEPVGWFVFWBWEVFWBHWEWFWAUMZAEWAAUKWFWDWGAWFVJSEWDWGNVJUNVJUOTRUP
      UQURUSVOVSVLVHVNVJAUTVCVAVBTVDVE $.
      $( [24-Jun-2013] $)

    $( A nonempty Tarski's class contains the whole finite cumulative
       hierarchy.  (This proof does not use ~ ax-inf .) $)
    tskr1om2 $p |- ( ( T e. Tarski /\ T =/= (/) ) -> U. ( R1 " om ) C_ T ) $=
      ( vy vx ctsk wcel c0 wne wa cr1 com cima cuni cv wrex eluni2 wss wtr con0
      wi syld cfv wceq wfun wfn r1fnon fnfun ax-mp fvelima mpan r1tr treq mpbii
      rexlimivw trss 3syl adantl tskr1om sseld 3exp adantr imp rexlimdva syl5bi
      tskss ssrdv ) ADEZAFGZHZBIJKZLZABMZVJEVKCMZEZCVINVHVKAEZCVKVIOVHVMVNCVIVH
      VLVIEZHVMVKVLPZVNVOVMVPSZVHVOVKIUAZVLUBZBJNZVLQZVQIUCZVOVTIRUDWBUERIUFUGB
      VLJIUHUIVSWABJVSVRQWAVKUJVRVLUKULUMVLVKUNUOUPVHVOVPVNSZVHVOVLAEZWCVHVIAVL
      AUQURVFWDWCSVGVFWDVPVNVLVKAVDUSUTTVATVBVCVE $.
      $( [22-Feb-2011] $)
  $}

  $( A nonempty Tarski's class is infinite. $)
  tskinf $p |- ( ( T e. Tarski /\ T =/= (/) ) -> om ~<_ T ) $=
    ( ctsk wcel wne com cr1 cima cen wbr cdom omex con0 cvv wf1 wss r111 omsson
    c0 wa f1imaen mp2an ensymi tskr1om wi relen brrelexi ssdomg endomtr sylancr
    mp2b syl ) ABCARDSZEFEGZHIUMAJIZEAJIUMEKLMFNELOUMEHIZPQLMEFKTUAZUBULUMAOZUN
    AUCUOUMMCUQUNUDUPUMEHUEUFUMAMUGUJUKEUMAUHUI $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their unordered pair
     is also an element of the class.  JFM CLASSES2 th. 3 (partly).
     (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by Mario
     Carneiro, 20-Jun-2013.) $)
  tskpr $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> { A , B } e. T ) $=
    ( ctsk wcel w3a cpr wss csdm wbr simp1 wa prssg ibi 3adant1 com cdom c0 wne
    ne0i tskinf sylan2 wi cfn prfi isfinite3 sdomdomtr mpani adantr mpd 3adant3
    mpbi tskssel syl111anc ) CDEZACEZBCEZFUOABGZCHZURCIJZURCEUOUPUQKUPUQUSUOUPU
    QLUSABCCCMNOUOUPUTUQUOUPLPCQJZUTUPUOCRSVACATCUAUBUOVAUTUCUPUOURPIJZVAUTURUD
    EVBABUEURUFULURPCDUGUHUIUJUKURCUMUN $.
    $( [22-Feb-2011] $)

  $( If ` A ` and ` B ` are members of a Tarski's class, their ordered pair is
     also an element of the class.  JFM CLASSES2 th. 4.  (Contributed by FL,
     22-Feb-2011.) $)
  tskop $p |- ( ( T e. Tarski /\ A e. T /\ B e. T )
    -> <. A , B >. e. T ) $=
    ( ctsk wcel w3a cop csn df-op simp1 tsksn 3adant3 tskpr syl111anc syl5eqel
    cpr ) CDEZACEZBCEZFZABGAHZABPZPZCABITQUACEZUBCEUCCEQRSJQRUDSACKLABCMUAUBCMN
    O $.
    $( [22-Feb-2011] $)

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A cross product of two parts of a Tarski's class is a part of the
       class.  (Contributed by FL, 22-Feb-2011.)  (The proof was shortened by
       Mario Carneiro, 20-Jun-2013.) $)
    tskxpss $p |- ( ( T e. Tarski /\ A C_ T /\ B C_ T )
    -> ( A X. B ) C_ T ) $=
      ( vz vx vy ctsk wcel wss cxp wa xpss12 wi cv cop wceq wrex elxp2 w3a syl
      tskop eleq1a 3expib rexlimdvv syl5bi ssrdv sstr expcom syl5 3impib ) CGHZ
      ACIZBCIZABJZCIZULUMKUNCCJZIZUKUOACBCLUKUPCIZUQUOMUKDUPCDNZUPHUSENZFNZOZPZ
      FCQECQUKUSCHZEFUSCCRUKVCVDEFCCUKUTCHZVACHZVCVDMZUKVEVFSVBCHVGUTVACUAVBCUS
      UBTUCUDUEUFUQURUOUNUPCUGUHTUIUJ $.
      $( [22-Feb-2011] $)
  $}

  ${
    $d T x y $.
    $( A Tarski's class is well-orderable.  (Contributed by Mario Carneiro,
       20-Jun-2013.) $)
    tskwe2 $p |- ( T e. Tarski -> E. x e. On x ~~ T ) $=
      ( vy ctsk wcel csdm wbr cpw crab wss cen con0 wrex wral tskssel2 ralrimiv
      cv wi 3exp rabss sylibr tskwe syl ) BDEZCQZBFGZCBHZIBJZAQBKGALMUDUFUEBEZR
      ZCUGNUHUDUJCUGUDUEUGEUFUIUEBOSPUFCUGBTUACABUBUC $.
      $( [20-Jun-2013] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is equipotent
       to ` A ` . $)
    inar1 $p |- ( A e. Inacc -> ( R1 ` A ) ~~ A ) $=
      ( vx vy vz wcel cr1 cfv cdom wbr con0 wceq syl wral wa csdm wi c0 cvv wss
      ccrd vw cina cen cv ciun wlim cwina inawina winaon winalim r1lim syl11anc
      cxp onelon sylan csuc eleq1 breq1d imbi12d wne ne0i 0sdomg syl5ibr breq1i
      fveq2 r10 syl6ibr 3syl wtr word eloni ordtr trsuc adantl cpw r1suc cardid
      ex fvex ensymi pwen ax-mp syl6eqbr wb winacard eleq2d cardsdom bitr3d ccf
      sylancr simp3bi pweq rcla4cv sylbird imp ensdomtr syl2an expr imim12d cun
      elina visset mpan iunex iundom ax-17 hbiu1 ssdomg mp2 domtr mp2an sylancl
      mprg com sylc domentr eqbrtrd ad2antlr wn wo wf wrex wfn sylibr wfun ffun
      cardf eleq1d ralimdva sylanbrc sseq2d syl5bi ad2antrl sylibd iunon onelss
      impr a1i iuneq1 mpd hbbr ssiun2 mpsyl endomtr vtoclgaf ssun2 xpdom2 ssun1
      unex xpdom1 ssexg syl6ss infxpidm eleq1a ordirr nsyli ad2ant2r simpll wex
      limomss ccom cres limord elon r1fnon dffn2 mpbi fco onss fssres ffn simpr
      simplll ontr1 biimpd embantd fvres fvco3 mp3an12 eqtrd ffnfv eleq2 biimpa
      syl12anc sylibrd eliun cardon onelssi reximdva expdimp ralrimiv resfunexg
      syl5 feq1 fveq1 rexbidv ralbidv anbi12d cla4ev ee12an syld simp2bi sseq1d
      cfflb ontri1 mt2d ioran eqcom ordequn mtod sylsyld iunss unssd id uneq12d
      cif 0elon elimel elisseti onun2i dedth adantr onsseleq orcomd ord simprbi
      mpbid iscard breq1 domsdomtr exp43 com4l tfinds2 imp3a sdomdom raleqbi1dv
      mpcom ralrimiva breq2 xpeq12 anidms breq12d vtoclg winainf infxpen onssr1
      sbth ) AUBEZAFGZAHIZAVUIHIZVUIAUCIVUHVUIAAUMZHIVULAUCIZVUJVUHVUIBABUDZFGZ
      UEZVULHVUHAJEZAUFZVUIVUPKVUHAUGEZVUQAUHZAUIZLZVUHVUSVURVUTAUJLBAJUKULVUHV
      UQVUOAHIZBAMZVUPVULHIZVVBVUHVVCBAVUHVUNAEZNZVUOAOIZVVCVUNJEZVVGVVHVUHVUQV
      VFVVIVVBAVUNUNUOVVIVUHVVFVVHVVFVVHPQAEZQFGZAOIZPZCUDZAEZVVNFGZAOIZPZVVNUP
      ZAEZVVSFGZAOIZPZVUHBCVUNQKZVVFVVJVVHVVLVUNQAUQVWDVUOVVKAOVUNQFVEURUSVUNVV
      NKZVVFVVOVVHVVQVUNVVNAUQVWEVUOVVPAOVUNVVNFVEURUSVUNVVSKZVVFVVTVVHVWBVUNVV
      SAUQVWFVUOVWAAOVUNVVSFVEURUSVUHVUSVUQVVMVUTVVAVUQVVJQAOIZVVLVVJVWGVUQAQUT
      ZAQVAAJVBVCVVKQAOVFVDVGVHVVNJEZVUHVVRVWCPVWIVUHNVVTVVOVVQVWBVUHVVTVVOPZVW
      IVUHVUQAVIZVWJVVBVUQAVJZVWKAVKZAVLLVWKVVTVVOAVVNVMVRVHVNVWIVUHVVQVWBVWIVW
      AVVPTGZVOZUCIVWOAOIZVWBVUHVVQNVWIVWAVVPVOZVWOUCVVNVPVVPVWNUCIZVWQVWOUCIVW
      NVVPVVNFVSZVVPVQVTZVVPVWNVVPTVSZWAWBWCVUHVVQVWPVUHVVQVWNAEZVWPVUHVUSVXBVV
      QWDVUTVUSVWNATGZEZVXBVVQVUSVXCAVWNAWEZWFVUSVVPREZVUQVXDVVQWDZVWSVVAVVPARJ
      WGZWJWHLVUHDUDZVOZAOIZDAMZVXBVWPPVUHVWHAWIGZAKZVXLDAXAZWKVXKVWPDVWNAVXIVW
      NKVXJVWOAOVXIVWNWLURWMLWNWOVWAVWOAWPWQWRWSVRVVFVUNUFZVUHVVRCVUNMZVVHVVFVX
      PVUHVXQVVHVVFVXPNZVUHVXQNZNZVUOVUNCVUNVWNUEZWTZHIZVYBAOIZVVHVXPVYCVVFVXSV
      XPVUODVUNVXIFGZUEZVYBHVUNREZVXPVUOVYFKBXBZDVUNRUKXCVXPVYFVUNVYAUMZHIZVYIV
      YBHIZVYFVYBHIVYEVYAHIZVYJDVUNDVUNVYAVYEVYHCVUNVWNVYHVXAXDZVXIFVSXEVVPVYAH
      IZVYLCBVXIVUNVUNVXIECXFCUAVYEVYAHUAUDZVYEECXFVYOHECXFCUAVUNVWNXGUUAVVNVXI
      KVVPVYEVYAHVVNVXIFVEURVVNVUNEZVWRVWNVYAHIZVYNVWTVWNREVYPVWNVYASVYQVXACVUN
      VWNUUBVWNVYARXHUUCVVPVWNVYAUUDWJUUEXMVXPVYIVYBVYBUMZHIZVYRVYBUCIZVYKVYIVU
      NVYBUMZHIZWUAVYRHIZVYSVYAVYBHIZWUBVYAREVYAVYBSWUDVYMVYAVUNUUFVYAVYBRXHXIV
      YAVYBVUNVUNVYAVYHVYMUUIZVYHUUGWBVUNVYBHIZWUCVYGVUNVYBSWUFVYHVUNVYAUUHZVUN
      VYBRXHXIVUNVYBVYBWUEWUEUUJWBVYIWUAVYRXJXKVXPXNVYBHIZVYTVXPXNREZXNVYBSWUHV
      XPXNVUNSVYGWUIVUNUUTZVYHXNVUNRUUKXLVXPXNVUNVYBWUJWUGUULXNVYBRXHXOVYBWUEUU
      MLVYIVYRVYBXPWJVYFVYIVYBXJWJXQXRVXTVYBAEZVYDVXTVYBAKZXSWUKVXTWULAVUNKZAVY
      AKZXTZVXTWUMXSZWUNXSWUOXSVVFVUHWUPVXPVXQVVFVUHWUPVVFWUMAAEZVUHVUNAAUUNVUH
      VUQVWLWUQXSVVBVWMAUUOVHUUPWOUUQVXTWUNVVFVVFVXPVXSUURZVXTWUNAVUNSZVVFXSZVX
      TWUNVXMVUNSZWUSVXTWUNVUNAVYOYAZVXIVVNVYOGZSZCVUNYBZDAMZNZUAUUSZWVAVXTVUNA
      TFUVAZVUNUVBZYAZWUNVXIVVNWVJGZSZCVUNYBZDAMZWVHVXTWVJVUNYCZWVLAEZCVUNMZWVK
      VXTVVIVUNJWVJYAZWVPVXPVVIVVFVXSVXPVUNVJZVVIVUNUVCVUNVYHUVDYDZXRZVVIJJWVIY
      AZVUNJSWVSRJTYAZJRFYAZWWCYGFJYCWWEUVEJFUVFUVGZJRJTFUVHXKZVUNUVIJJVUNWVIUV
      JWJVUNJWVJUVKVHVXRVUHVXQWVRVXRVUHNZVVRWVQCVUNWWHVYPNZVVRVXBWVQWWIVVOVVQVX
      BWWIVUQVYPVVFVVOVUHVUQVXRVYPVVBXRZWWHVYPUVLVVFVXPVUHVYPUVMVUQVYPVVFNVVOVV
      NVUNAUVNWOUWDWWIVVQVXBWWIVXDVVQVXBWWIVXFVUQVXGVWSWWJVXHWJWWIVXCAVWNVUHVXC
      AKZVXRVYPVUHVUSWWKVUTVXELZXRWFWHUVOUVPZWWIWVLVWNAWWHVVIVYPWVLVWNKVXPVVIVV
      FVUHWWAXRVVIVYPNZWVLVVNWVIGZVWNVYPWVLWWOKVVIVVNVUNWVIUVQVNWWNVWIWWOVWNKZV
      UNVVNUNTYEZWWEVWIWWPWWDWWQYGRJTYFWBWWFJRVVNTFUVRUVSLUVTZUOYHUWEYIYQCVUNAW
      VJUWAYJVXTVVIWUNWVOPWWBVVIWUNWVOVVIWUNNWVNDAVVIWUNVXIAEZWVNWUNWWSNVXIVYAE
      ZVVIWVNWUNWWSWWTAVYAVXIUWBUWCWWTVXIVWNEZCVUNYBVVIWVNCVXIVUNVWNUWFVVIWXAWV
      MCVUNWXAWVMWWNVXIVWNSVWNVXIVVPUWGZUWHWWNWVLVWNVXIWWRYKVCUWIYLUWMUWJUWKVRL
      WVGWVKWVONUAWVJWVIYEZVYGWVJREWWCWXCWWGJJWVIYFWBVYHWVIVUNRUWLXKVYOWVJKZWVB
      WVKWVFWVOVUNAVYOWVJUWNWXDWVEWVNDAWXDWVDWVMCVUNWXDWVCWVLVXIVVNVYOWVJUWOYKU
      WPUWQUWRUWSUWTVXTVUQVVIWVHWVAPVUHVUQVXRVXQVVBYMZWWBDCAVUNUAUXDULUXAVUHWVA
      WUSWDVXRVXQVUHVXMAVUNVUHVWHVXNVXLVXOUXBUXCYMYNVXTVUQVVIWUSWUTWDWXEWWBAVUN
      UXEULYNUXFWUMWUNUXGYJVXTVVIVYAJEZWULWUOPWWBVWNJEZWXFCVUNCVUNVWNVYHVXAYOWX
      GVYPWXBYRXMWULAVYBKZVVIWXFNWUOVYBAUXHVVIWVTVYAVJWXHWUOPWXFVUNVKVYAVKAVUNV
      YAUXIWQYLXLUXJVXTWULWUKVXTWUKWULVXTVYBASZWUKWULXTZVXTVUNVYAAVXTVUQVVFVUNA
      SWXEWURAVUNYPXOVXTVWNASZCVUNMZVYAASVXRVUHVXQWXLWWHVVRWXKCVUNWWIVUQVVRVXBW
      XKWWJWWMAVWNYPUXKYIYQCVUNVWNAUXLYDUXMVXRVYBJEZVUQWXIWXJWDVXSVXPWXMVVFVXPV
      VIWXMWWAVVIWXMVVIVUNQUXPZCWXNVWNUEZWTZJEVUNQVUNWXNKZVYBWXPJWXQVUNWXNVYAWX
      OWXQUXNCVUNWXNVWNYSUXOYHWXNWXOVUNQJUXQUXRZWXGWXOJECWXNCWXNVWNWXNJWXRUXSVX
      AYOWXGVVNWXNEWXBYRXMUXTUYALVNVUHVUQVXQVVBUYBVYBAUYCWQUYGUYDUYEYTVXTWWKVXI
      AOIZDAMZWUKVYDPVUHWWKVXRVXQWWLYMWWKVUQWXTDAUYHUYFWXSVYDDVYBAVXIVYBAOUYIWM
      VHYTVUOVYBAUYJULUYKUYLUYMUYNUYQVUOAUYOLUYRVUOVXIHIZBVXIMZBVXIVUOUEZVXIVXI
      UMZHIZPVVDVVEPDAJVXIAKZWYBVVDWYEVVEWYAVVCBVXIAVXIAVUOHUYSUYPWYFWYCVUPWYDV
      ULHBVXIAVUOYSWYFWYDVULKVXIAVXIAUYTVUAVUBUSBVXIVXIVUODXBZWYGVUNFVSXEVUCXOX
      QVUHVUQXNASZVUMVVBVUHVUSWYHVUTAVUDLAVUEULVUIVULAXPULVUHVUQAVUISZVUKVVBVUH
      VUSVUQWYIVUTVVAAVUFVHAVUIJXHXOVUIAVUGUL $.
      $( [6-Jun-2013] $)
  $}

  ${
    $( The set of hereditarily finite sets is countable.  TODO:  Prove without
       AC and Infinity. $)
    r1om $p |- ( R1 ` om ) ~~ om $=
      ( com cina wcel cr1 cfv cen wbr omina inar1 ax-mp ) ABCADEAFGHAIJ $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( Any set must be at least as large as the cofinality of its rank, because
       the ranks of the elements of ` A ` form a cofinal map into
       ` ( rank `` A ) ` . $)
    rankcf $p |- -. A ~< ( cf ` ( rank ` A ) ) $=
      ( vx vy vw vz crnk cfv c0 wceq con0 wrex cvv wcel wa ccf wbr cdom syl wb
      wi cv csuc wlim csdm wn rankon onzsl mpbi 0dom fveq2 syl6eq breq1d mpbiri
      w3o cf0 domnsym c1o cfsuc sylan9eqr wne nsuceq0 neeq1 0elon rankonid cima
      necon3i cr1 rankvaln necon1ai breq2 bibi12d visset 0sdom1dom 0sdom bitr3i
      cuni vtoclg mpbird adantl eqbrtrd rexlimiva csn nlim0 limeq mtbiri fveq2d
      cab con4i breq12 mpdan rexeq mpancom imbi12d cmpt crn eqid rnmpt cen cfon
      abbidv sdomdom rcla4ev sylancr ondomen wfo fnmpt fvex mprg dffn4 fodomnum
      wfn a1i mpi 3syl syl5eqbrr wss r1elssi ssel2 sylan ranksnb rankelb limsuc
      sylibd imp eqeltrd eleq1a rexlimdva abssdv ciun snex dfiun2 eqtr3i fveq2i
      id iunid snwf abrexexg eleq1 sseq1 r1elss rankuni2b syl5eqr abrexco eqtri
      unieqi syl6req cfslb syl111anc domtr ex syld pm2.01d 3jaoi ax-mp ) AFGZHI
      ZUUOBUAZUBZIZBJKZUUOLMZUUOUCZNZUNZAUUOOGZUDPZUEZUUOJMUVDAUFBUUOUGUHUUPUVG
      UUTUVCUUPUVEAQPZUVGUUPUVHHAQPAUIUUPUVEHAQUUPUVEHOGHUUOHOUJUOUKULUMUVEAUPZ
      RUUTUVHUVGUUSUVHBJUUQJMZUUSNUVEUQAQUUSUVJUVEUUROGUQUUOUUROUJUUQURUSUUSUQA
      QPZUVJUUSUUOHUTZUVKUUSUVLUURHUTUUQVAUUOUURHVBUMUVLUVKAHUTZAHUUOHAHIUUOHFG
      ZHAHFUJHJMUVNHIVCHVDUHUKVFUVLAVGJVEVPZMZUVKUVMSZUVPUUOHAVHZVIUQCUAZQPZUVS
      HUTZSUVQCAUVOUVSAIZUVTUVKUWAUVMUVSAUQQVJUVSAHVBVKUVTHUVSUDPUWAUVSCVLZVMUV
      SUWCVNVOVQRVRRVSVTWAUVIRUVBUVGUVAUVBUVFUVBUVFDUAZUUQWBZFGZIZBAKZDWGZAQPZU
      VGUVBUVPUVFUWJTZUVPUVBUVPUEUUPUVBUEUVRUUPUVBHUCWCUUOHWDWERWHZUVSUVSFGZOGZ
      UDPZUWGBUVSKZDWGZUVSQPZTUWKCAUVOUWBUWOUVFUWRUWJUWBUWNUVEIUWOUVFSUWBUWMUUO
      OUVSAFUJWFUVSAUWNUVEUDWIWJUWQUWIIUWBUWRUWJSUWBUWPUWHDUWGBUVSAWKWTUWQUWIUV
      SAQWIWLWMUWOUWQBUVSUWFWNZWOZUVSQBDUVSUWFUWSUWSWPZWQUWOUVSUUQQPZBJKZUUQUVS
      WRPBJKZUWTUVSQPZUWOUWNJMUVSUWNQPZUXCUWMWSUVSUWNXAUXBUXFBUWNJUUQUWNUVSQVJX
      BXCBBUVSXDUXDUVSUWTUWSXEZUXEUWSUVSXKZUXGUWFLMZUXHBUVSBUVSUWFUWSLUXAXFUXIU
      UQUVSMUWEFXGXLXHUVSUWSXIUHBUVSUWTUWSUWCXJXMXNXOVQRUVBUVEUWIQPZUWJUVGTUVBU
      VBUWIUUOXPUWIVPZUUOIZUXJUVBYNUVBUWHDUUOUVBUWGUWDUUOMZBAUVBUUQAMZNZUWFUUOM
      UWGUXMTUXOUWFUUQFGZUBZUUOUXOUUQUVOMZUWFUXQIUVBAUVOXPZUXNUXRUVBUVPUXSUWLAX
      QZRAUVOUUQXRZXSUUQXTRUVBUXNUXQUUOMZUVBUXNUXPUUOMZUYBUVBUVPUXNUYCTUWLUUQAY
      ARUUOUXPYBYCYDYEUWFUUOUWDYFRYGYHUVBUVPUXLUWLUVPUUOEUVSUWEIZBAKZCWGZEUAZFG
      ZYIZUXKUVPUUOUYFVPZFGZUYIUYJAFBAUWEYIUYJABCAUWEUUQYJZYKBAYOYLYMUVPUYFUVOM
      ZUYKUYIIUVPUYMUYFUVOXPZUVPUYECUVOUVPUYDUVSUVOMZBAUVPUXNNZUWEUVOMZUYDUYOTU
      YPUXRUYQUVPUXSUXNUXRUXTUYAXSUUQYPRUWEUVOUVSYFRYGYHUVPUYFLMUYMUYNSZBCAUWEU
      VOYQUYGUVOMZUYGUVOXPZSUYREUYFLUYGUYFIUYSUYMUYTUYNUYGUYFUVOYRUYGUYFUVOYSVK
      UYGEVLYTVQRVREUYFUUARUUBUYIUWDUYHIEUYFKDWGZVPUXKEDUYFUYHUYGFXGYKVUAUWIDEC
      BAUWEUYHUWFUYLUYGUWEFUJUUCUUEUUDUUFRUUOUWIAFXGUUGUUHUXJUWJUVGUXJUWJNUVHUV
      GUVEUWIAUUIUVIRUUJRUUKUULVSUUMUUN $.
      $( [27-May-2013] $)
  $}

  ${
    $d A w x y z $.
    $( ` ( R1 `` A ) ` for ` A ` a strongly inaccessible cardinal is a Tarski's
       class. $)
    inatsk $p |- ( A e. Inacc -> ( R1 ` A ) e. Tarski ) $=
      ( vx vy wcel cr1 cfv wss wa wral wbr wo con0 wceq syl sylbid imp csdm ccf
      wi wb cina cpw cen ctsk cwina inawina wrex ciun wlim winaon winalim r1lim
      cv syl11anc eleq2d eliun syl6bb csuc onelon sylan limsuc r1ord2 rexlimdva
      r1pw sseld cuni elssuni r1tr2 syl6ss jca ralrimiva r1suc rankr1ai syl6bir
      crnk fvex elsuc sylib orcomd wn elpwi ssdomg mpd ad2antlr rankcf fveq2 c0
      cdom wne w3a elina simp2 sylbi sylan9eqr breq2d mtbii sdomentr exp3acom23
      inar1 sylc adantr mtod adantlr bren2 sylanbrc cima r1elwf biimprd orim12d
      ex rankr1ag eltsk2 ) AUADZBUMZUBZAEFZGZXOXPDZHZBXPIXNXPUCJZXNXPDZKZBXPUBZ
      IXPUDDXMXSBXPXMYAHZXQXRYDXRXQXMYAXRXMAUEDZYAXRSAUFZYEYAXNCUMZEFZDZCAUGZXR
      YEYAXNCAYHUHZDYJYEXPYKXNYEALDZAUIZXPYKMAUJZAUKZCALULUNUOCXNAYHUPUQYEYIXRC
      AYEYGADZHZYIXOYGURZEFZDZXRYQYGLDZYIYTTYEYLYPUUAYNAYGUSUTXNYGVDNYQYSXPXOYE
      YPYSXPGZYEYPYRADZUUBYEYMYPUUCTYOAYGVANYEYLUUCUUBSYNYRAVBNOPVEOVCONPZXRXOX
      PVFXPXOXPVGAVHVINUUDVJVKXMYBBYCXMXNYCDZHZXNVOFZAMZUUGADZKYBUUFUUIUUHUUFUU
      GAURZDZUUIUUHKXMUUEUUKXMUUEXNUUJEFZDZUUKXMYLUUMUUETXMYEYLYFYNNZYLUULYCXNA
      VLUONZXNUUJVMVNPUUGAXNVOVPVQVRVSUUFUUHXTUUIYAUUFUUHXTUUFUUHHXNXPWHJZXNXPQ
      JZVTZXTUUEUUPXMUUHUUEXNXPGUUPXNXPWAXNXPYCWBWCWDXMUUHUURUUEXMUUHHZUUQXNAQJ
      ZUUSXNUUGRFZQJUUTXNWEUUSUVAAXNQUUHXMUVAARFZAUUGARWFXMAWGWIZUVBAMZXOAQJBAI
      ZWJUVDBAWKUVCUVDUVEWLWMWNWOWPXMUUQUUTSZUUHXMYLXPAUCJZUVFUUNAWSYLUUQUVGUUT
      XNXPALWQWRWTXAXBXCXNXPXDXEXJUUFYAUUIUUFXNELXFVFDZYLYAUUITXMUUEUVHXMUUEUUM
      UVHUUOXNUUJXGVNPXMYLUUEUUNXAXNAXKUNXHXIWCVKBXPAEVPXLXE $.
      $( [8-Jun-2013] $)
  $}

  $( The set of hereditarily finite sets is a Tarski's class.  (The
     Tarski-Grothendieck Axiom is not needed for this theorem.) $)
  r1omtsk $p |- ( R1 ` om ) e. Tarski $=
    ( com cina wcel cr1 cfv ctsk omina inatsk ax-mp ) ABCADEFCGAHI $.
    $( [28-May-2013] $)

  ${
    $d T x y $.  $d A x $.
    $( A Tarski's class contains all ordinals smaller than it. $)
    tskord $p |- ( ( T e. Tarski /\ A e. On /\ A ~< T ) -> A e. T ) $=
      ( vx vy con0 wcel csdm wbr cv wa wceq breq1 anbi2d eleq1 imbi12d wral cvv
      wi wss imp ctsk simplrl cdom visset onelss ssdomg mpsyl adantlr domsdomtr
      simplrr syl11anc pm2.27 ralimdva dfss3 tskssel 3exp syl5bir com23 syld ex
      adantl tfis2 vtoclga 3impib 3com12 ) AEFZBUAFZABGHZABFZVFVGVHVIVGCIZBGHZJ
      ZVJBFZRZVGVHJZVIRCAEVJAKZVLVOVMVIVPVKVHVGVJABGLMVJABNOVNVGDIZBGHZJZVQBFZR
      ZCDVJVQKZVLVSVMVTWBVKVRVGVJVQBGLMVJVQBNOVJEFZVLWADVJPZVMWCVLWDVMRWCVLJZWD
      VTDVJPZVMWEWAVTDVJWEVQVJFZJZVGVRWAVTRWCVGVKWGUBWHVQVJUCHZVKVRWCWGWIVLVQQF
      WCWGJVQVJSZWIDUDWCWGWJVJVQUETVQVJQUFUGUHWCVGVKWGUJVQVJBUIUKVSVTULUKUMVLWF
      VMRZWCVGVKWKVGWFVKVMWFVJBSZVGVKVMRDVJBUNVGWLVKVMVJBUOUPUQURTVAUSUTURVBVCV
      DVE $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d T w x y z $.
    $( An even more direct relationship than ~ r1tskina to get an inacessible
       cardinal out of a Tarski's class: the size of any nonempty Tarski's
       class is an inaccessible cardinal. $)
    tskcard $p |- ( ( T e. Tarski /\ T =/= (/) ) -> ( card ` T ) e. Inacc ) $=
      ( vx vy wcel c0 wa cfv ccf csdm wbr cmap wss con0 cdom cen cvv syl adantr
      wi syl11anc vz ctsk wne ccrd wceq cpw wral cina cardeq0 necon3bid biimpar
      cv w3a wn cale crab cint cmpt eqid pwcfsdom com visset pwex canth2 cardon
      co simpl onelon adantl cardsdomelir tskord syl111anc tskpw tskpwss syldan
      mpan ssdomg mpsyl wrex tskssel2 3exp ralrimiv rabss sylibr cardid2 ensymg
      tskwe mpd domentr sdomdomtr ax-mp sylancr ralrimiva inawinalem winainflem
      mp3an2 sylan2 cardidm cardaleph sylancl fveq2d breq12d mpbiri oprex simp1
      opreq12d simp3 wf fvex elmap fssxp sylbi ex ssrdv cfle sstr tskxpss com23
      cxp mpdi sstr2 syl2im simp2 wfn ffn fndmeng ensym ensdomtr syl2an tskssel
      sylc 3expia domnsym mt2d wo wb cfon onsseleq mp2an mpbi ori 3jca elina )
      AUBDZAEUCZFZAUDGZEUCZUUGHGZUUGUEZBULZUFZUUGIJZBUUGUGZUMUUGUHDUUFUUHUUJUUN
      UUDUUHUUEUUDUUGEAEAUBUIUJUKZUUFUUIUUGDZUNUUJUUFUUPUUGUUGUUIKVFZIJZUUFUURU
      UGUUKUOGLBMUPUQZUOGZUUTUUTHGZKVFZIJUAUUSCUAUVAUUKUAULCULZGNJBMUPURZBUVDUS
      UTUUFUUGUUTUUQUVBIUUFVAUUGLZUUGUDGUUGUEUUGUUTUEUUFUUHUUNUVEUUOUUDUUNUUEUU
      DUUMBUUGUUDUUKUUGDZFZUULUULUFZIJZUVHUUGNJZUUMUULUUKBVBZVCZVDUVGUVHANJZAUU
      GOJZUVJUVHPDUVGUVHALZUVMUULUVLVCUUDUVFUUKADZUVOUVGUUDUUKMDZUUKAIJZUVPUUDU
      VFVGUVFUVQUUDUUGMDZUVFUVQAVEZUUGUUKVHVPVIUVFUVRUUDUUKAVJVIUUKAVKVLZUUDUVP
      UULADUVOUUKAVMUULAVNVOVOUVHAPVQVRUUDUVNUVFUUDUUGAOJZUVNUUDUVCAOJCMVSZUWBU
      UDUVRBAUFZUPALZUWCUUDUVRUVPSZBUWDUGUWEUUDUWFBUWDUUDUUKUWDDUVRUVPUUKAVTWAW
      BUVRBUWDAWCWDBCAWGQCAWEQUUGAUBWFWHZRUVHAUUGWITUVSUVIUVJFUUMSUVTUULUVHUUGM
      WJWKWLWMRZUUNUUHUUKUVCIJCUUGVSBUUGUGZUVEUVSUUNUWISUVTBCUUGWNWKUUHUVSUWIUV
      EUVTBCUUGWOWPWQTAWRBUUGWSWTZUUFUUGUUTUUIUVAKUWJUUFUUGUUTHUWJXAXFXBXCUUDUU
      PUURUNZSUUEUUDUUPUWKUUDUUPFZUUQUUGNJZUWKUWLUUQANJZUVNUWMUUQPDUWLUUQALUWNU
      UGUUIKXDUWLBUUQAUUDUUPUUKUUQDZUVPUUDUUPUWOUMZUUDUUKALZUVRUVPUUDUUPUWOXEZU
      WPUWOUUDUWQUUDUUPUWOXGZUWRUWOUUKUUIUUGXSZLZUUDUWTALZUWQUWOUUIUUGUUKXHZUXA
      UUGUUIUUKAUDXIUUGHXIZXJZUUIUUGUUKXKXLUUDUUGALZUXBUUDBUUGAUUDUVFUVPUWAXMXN
      UUDUXFUUIALZUXBUUIUUGLZUXFUXGUUGXOZUUIUUGAXPVPUUDUXGUXFUXBUUDUXGUXFUXBUUI
      UUGAXQWAXRXTWHUUKUWTAYAYBYKUWPUWOUUPUVRUWSUUDUUPUWOYCUWOUUKUUIOJZUUIAIJUV
      RUUPUWOUUIUUKOJZUXJUWOUXCUXKUXEUXCUUKUUIYDUUIPDUXKUUIUUGUUKYEUXDUUIPUUKYF
      WTXLUUIUUKUVKYGQUUIAVJUUKUUIAYHYITUUKAYJVLYLXNUUQAPVQVRUUDUVNUUPUWGRUUQAU
      UGWITUUQUUGYMQXMRYNUUPUUJUXHUUPUUJYOZUXIUUIMDUVSUXHUXLYPUUGYQUVTUUIUUGYRY
      SYTUUAQUWHUUBBUUGUUCWD $.
      $( [9-Jun-2013] $)
  $}

  ${
    $d A x $.
    $( There is a direct relationship between transitive Tarski's classes and
       inacessible cardinals: the Tarski's classes that occur in the cumulative
       hierarchy are exactly at the strongly inaccessible cardinals. $)
    r1tskina $p |- ( A e. On ->
        ( ( R1 ` A ) e. Tarski <-> ( A = (/) \/ A e. Inacc ) ) ) $=
      ( vx con0 wcel cr1 cfv ctsk c0 wceq cina wo wne ccrd cvv cen wbr syl11anc
      wa wn csdm df-ne fvex simplr simpll crnk csuc cima cuni onwf eqid rankr1c
      sseli mpbii syl simpld rankonid biimpi fveq2d eleq2d mtbid adantl cpw wss
      onssr1 elpwg mpbird tsken sylan2 mt3d carden2b mpsyl cv wral simpl adantr
      ord sseld imp cdom visset canth2 pwex tskpwss ssdomg wi sdomdomtr sylancr
      ax-mp ensym sdomentr syl12anc ralrimiva iscard eqtr3d r10 on0eln0 biimpar
      sylanbrc r1sdom syl5eqbrr 0sdom sylib adantlr tskcard eqeltrrd ex syl5bir
      syldan orrd 0tsk fveq2 syl6eq eleq1d mpbiri inatsk jaoi a1i impbid ) ACDZ
      AEFZGDZAHIZAJDZKZXSYAYDXSYARZYBYCYBSAHLZYEYCAHUAYEYFYCYEYFRZXTMFZAJYGAMFZ
      YHAXTNDZYGAXTOPZYIYHIAEUBZYGYAXSYKXSYAYFUCZXSYAYFUDYAXSRZYKAXTDZXSYOSYAXS
      AAUEFZEFZDZYOXSYRSZAYPUFEFDZXSAECUGUHZDZYSYTRZCUUAAUIULUUBYPYPIUUCYPUJAYP
      UKUMUNUOXSYQXTAXSYPAEXSYPAIAUPUQURUSUTVAYNYKYOXSYAAXTVBDZYKYOKXSUUDAXTVCZ
      AVDZAXTCVEVFAXTVGVHVPVIZQAXTNVJVKYEYIAIZYFYEXSBVLZATPZBAVMUUHXSYAVNYEUUJB
      AYEUUIADZRZXSUUIXTTPZXTAOPZUUJXSYAUUKUDZUULYAUUIXTDZUUMXSYAUUKUCZYEUUKUUP
      YEAXTUUIXSUUEYAUUFVOVQVRYAUUPRZUUIUUIVBZTPZUUSXTVSPZUUMUUIBVTZWAUUSNDUURU
      USXTVCUVAUUIUVBWBUUIXTWCUUSXTNWDVKYJUUTUVARUUMWEYLUUIUUSXTNWFWHWGQUULYAXS
      UUNUUQUUOYNYKUUNUUGAXTYLWIUNQXSUUMUUNRUUJUUIXTACWJVRWKWLBAWMWRVOWNYGYAXTH
      LZYHJDYMXSYFUVCYAXSYFRZHXTTPUVCUVDHHEFZXTTWOXSYFHADZUVEXTTPXSUVFYFAWPWQAH
      WSXHWTXTYLXAXBXCXTXDQXEXFXGXIXFYDYAWEXSYBYAYCYBYAHGDXJYBXTHGYBXTUVEHAHEXK
      WOXLXMXNAXOXPXQXR $.
      $( [8-Jun-2013] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F y $.
    $( A version of ~ fodom that doesn't require the Axiom of Choice ~ ax-ac .
       This version uses a sethood predicate as an antecedent.  (Contributed by
       Mario Carneiro, 23-Jun-2013.) $)
    fodomnumg $p |- ( ( A e. V /\ E. x e. On x ~~ A
        /\ F : A -onto-> B ) -> B ~<_ A ) $=
      ( vy wcel cv cen wbr con0 wrex wfo cdom wa wi wceq breq2 rexbidv foeq2
      anbi12d imbi12d visset fodomnum imp vtoclg 3impib ) BEGAHZBIJZAKLZBCDMZCB
      NJZUHFHZIJZAKLZUMCDMZOZCUMNJZPUJUKOZULPFBEUMBQZUQUSURULUTUOUJUPUKUTUNUIAK
      UMBUHIRSUMBCDTUAUMBCNRUBUOUPURAUMCDFUCUDUEUFUG $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A f x y z $.  $d T f x y z $.
    $( The union of an element of a transitive Tarski's class is in the set.
       (Contributed by Mario Carneiro, 22-Jun-2013.) $)
    tskuni $p |- ( ( T e. Tarski /\ Tr T /\ A e. T ) -> U. A e. T ) $=
      ( vf vz vx vy wcel cen wbr cv wa wceq wrex csdm con0 syl wi cdom cvv wss
      ctsk wtr w3a cuni wn ccrd cfv wf1o wex cab wne ccf tsksdom tskwe2 cardid2
      cima ensymg adantr cardon sdomentr ax-mp syl11anc sdomdom rcla4ev sylancr
      mpd breq2 ondomen cmpt crn eqid rnmpt wfo wfn fnmpt visset a1i mprg dffn4
      imaexg mpbi fodomnumg mp3an3 syl5eqbrr sylan2 sylan anabss3 adantll mpdan
      domsdomtr cina c0 ne0i tskcard cpw wral elina simp2 breqtrrd 3adant2 wlim
      sylbi cwina inawina winalim 3syl eqeq1 rexbidv elab imassrn forn syl5sseq
      f1ofo ad2antlr wf1 f1of1 elssuni f1imaen syl2an trss 3adant1 ssel2 simpl1
      imp fvex adantlr ensdomtr sseq1 breq1 anbi12d biimprcd syl5bi ralrimiv ex
      jca ax-17 syl6 com23 syld expcom rexlimdva cfslb2n ciun dfiun2 a1d sylibr
      iunss foelrn wf fof eluni2 hbiu1 hbel wfun cdm sseq2d syl5ibr ffun jctild
      funfvima2 ssiun2 sseld sylcom rexlimd eleq1a rexlimdv ssrdv eqssd syl5eqr
      fdm necon3ai pm2.01d nexdv entr bren sylib 3ad2ant1 wo uniss df-tr biimpi
      mtod sstr elpw2g tsken sylbird syl5 3impib ord ) BUAGZBUBZABGZUCZAUDZBHIZ
      UEUWNBGZUWMUWOUWNBUFUGZCJZUHZCUIZUWMUWSCUWMUWSUWMUWSUWSUEZUWMUWSKZDJZUWRE
      JZUPZLZEAMZDUJZUDZUWQUKZUXAUXBUXHUWQULUGZNIZUXJUWMUXLUWSUWJUWLUXLUWKUWJUW
      LKZUXHUWQUXKNUXMAUWQNIZUXHUWQNIZUXMABNIZBUWQHIZUXNABUMUWJUXQUWLUWJUWQBHIZ
      UXQUWJUXDBHIEOMUXREBUNEBUOPUWQBUAUQVFZURUWQOGZUXPUXQKUXNQBUSZABUWQOUTVAVB
      UWLUXNUXOUWJUWLUXNUXOUWLUXNKUXHARIZUXNUXOUXNUWLUXDAHIEOMZUYBUXNAUXDRIZEOM
      ZUYCUXNUXTAUWQRIZUYEUYAAUWQVCUYDUYFEUWQOUXDUWQARVGVDVEEEAVHPUWLUYCKUXHEAU
      XEVIZVJZAREDAUXEUYGUYGVKZVLUWLUYCAUYHUYGVMZUYHARIUYGAVNZUYJUXESGZUYKEAEAU
      XEUYGSUYIVOUYLUXDAGZUWRSGUYLCVPUWRUXDSVTVAZVQVRAUYGVSWAEAUYHUYGBWBWCWDWEU
      XHAUWQWJWFWGWHWIUXMUWQWKGZUXKUWQLZUWLUWJBWLUKUYOBAWMBWNWEZUYOUWQWLUKZUYPU
      XDWOUWQNIEUWQWPZUCUYPEUWQWQUYRUYPUYSWRXBZPWSWTURUXBUWQXAZFJZUWQTZVUBUXKNI
      ZKZFUXHWPUXLUXJQUXBUYOUWQXCGVUAUWMUYOUWSUWJUWLUYOUWKUYQWTURZUWQXDUWQXEXFU
      XBVUEFUXHVUBUXHGVUBUXELZEAMZUXBVUEUXGVUHDVUBFVPUXCVUBLUXFVUGEAUXCVUBUXEXG
      XHXIUXBVUGVUEEAUXBUYMKZUXEUWQTZUXEUXKNIZKZVUGVUEQVUIVUJVUKUWSVUJUWMUYMUWS
      UWRVJZUXEUWQUWRUXDXJUWSUWNUWQUWRVMZVUMUWQLUWNUWQUWRXMZUWNUWQUWRXKPXLZXNVU
      IUXEUWQUXKNVUIUXEUXDHIZUXDUWQNIZUXEUWQNIUWSUYMVUQUWMUWSUWNUWQUWRXOUXDUWNT
      ZVUQUYMUWNUWQUWRXPUXDAXQZUWNUWQUXDUWREVPXRXSWHUWMUYMVURUWSUWMUYMKZUXDBNIZ
      UXQVURVVAUXDBGZVVBUWMABTZUYMVVCUWKUWLVVDUWJUWKUWLVVDBAXTZYDYAABUXDYBWFVVA
      UWJVVCVVBUWJUWKUWLUYMYCZUXDBUMWFWIVVAUWJUXQVVFUXSPUWQSGVVBUXQKVURQBUFYEZU
      XDBUWQSUTVAVBYFUXEUXDUWQYGVBUXBUYPUYMUXBUYOUYPVUFUYTPURWSYOVUGVUEVULVUGVU
      CVUJVUDVUKVUBUXEUWQYHVUBUXEUXKNYIYJYKPUUAYLYMFUWQUXHVVGUUBVBVFUWSUXIUWQUW
      SUXIEAUXEUUCZUWQEDAUXEUYNUUDUWSVVHUWQUWSVUJEAWPVVHUWQTUWSVUJEAUWSVUJUYMVU
      PUUEYMEAUXEUWQUUGUUFUWSFUWQVVHUWSVUNVUBUWQGZVUBVVHGZQVUOVUNVVIVUBUXCUWRUG
      ZLZDUWNMZVVJVUNVVIVVMDUWNUWQVUBUWRUUHYNVUNUWNUWQUWRUUIZVVMVVJQUWNUWQUWRUU
      JVVNVVLVVJDUWNVVNUXCUWNGZVVKVVHGZVVLVVJQVVOUXCUXDGZEAMVVNVVPEUXCAUUKVVNVV
      QVVPEAVVNEYPEFFVVKVVHVUBVVKGEYPEFAUXEUULUUMVVNVVQUYMVVPVVNVVQUYMVVPQVVNVV
      QKUYMVVKUXEGZVVPVVNVVQUYMVVRQVVNUYMVVQVVRVVNUYMUWRUUNZUXDUWRUUOZTZKVVQVVR
      QVVNUYMVWAVVSUYMVWAVVNVUSVUTVVNVVTUWNUXDUWNUWQUWRUVJUUPUUQUWNUWQUWRUURUUS
      UXDUXCUWRUUTYQYRYDUYMUXEVVHVVKEAUXEUVAUVBUVCYNYRUVDYLVVKVVHVUBUVEYQUVFPYS
      PUVGUVHUVIUVKPYNUVLUVMUWJUWKUWOUWTQUWLUWOUWJUWTUWOUWJKUWNUWQHIZUWTUWJUWOU
      XQVWBUXSUWNBUWQUVNWEUWNUWQCVVGUVOUVPYTUVQUWBUWMUWOUWPUWJUWKUWLUWOUWPUVRZU
      WKUWLKUWNBTZUWJVWCUWKUWLVWDUWKUWLVVDVWDVVEVVDUWKVWDVVDUWNBUDZTVWEBTZVWDUW
      KABUVSUWKVWFBUVTUWAUWNVWEBUWCXSYTYSYDUWJVWDUWNBWOGZVWCUWNBUAUWDUWJVWGVWCU
      WNBUWEYNUWFUWGUWHUWIVF $.
      $( [22-Jun-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d T x y z $.
    $( A transitive Tarski's class is closed under small unions.  (Contributed
       by Mario Carneiro, 22-Jun-2013.) $)
    tskurn $p |- ( ( ( T e. Tarski /\ Tr T ) /\ A e. T /\ F : A --> T )
    -> U. ran F e. T ) $=
      ( vx ctsk wcel wss csdm wbr 3ad2ant3 cen con0 wrex wwe wex sylc syl111anc
      vy cv ween wtr wa wf w3a crn cuni simp1l simp1r frn cdom wfo simp2 tskwe2
      trss mpbird wess eximdv syl5com syl mpbid wfn ffn dffn4 fodomnumg tsksdom
      wb sylib syl11anc domsdomtr tskssel tskuni ) BEFZBUAZUBZABFZABCUCZUDZVLVM
      CUEZBFZVRUFBFVLVMVOVPUGZVLVMVOVPUHZVQVLVRBGZVRBHIZVSVTVPVNWBVOABCUIJVQVRA
      UJIZABHIZWCVQVODSZAKIDLMZAVRCUKZWDVNVOVPULZVQARSZNZROZWGVQVLABGZWLVTVQVMV
      OWMWAWIBAUNPVLBWJNZROZWMWLVLWOWFBKIDLMDBUMDBERTUOWMWNWKRABWJUPUQURPVQVOWL
      WGVFWIDABRTUSUTVPVNWHVOVPCAVAWHABCVBACVCVGJDAVRCBVDQVQVLVOWEVTWIABVEVHVRA
      BVIVHVRBVJQVRBVKQ $.
      $( [22-Jun-2013] $)
  $}

  $( Two ways of saying a class of ordinals is unbounded.  (Contributed by
     Mario Carneiro, 8-Jun-2013.) $)
  ssonprc $p |- ( A C_ On -> ( A e/ _V <-> U. A = On ) ) $=
    ( cvv wnel wcel wn con0 wss cuni wceq df-nel word ssorduni ordeleqon orcomd
    wo sylib ord elisset uniexb sylibr syl6 con1d onprc uniexg eleq1 syl5ib a1i
    wi mtoi impbid syl5bb ) ABCABDZEZAFGZAHZFIZABJUNUMUPUNUPULUNUPEUOFDZULUNUPU
    QUNUQUPUNUOKUQUPOALUOMPNQUQUOBDZULUOFRASTUAUBUPUMUHUNUPULFBDZUCULURUPUSABUD
    UOFBUEUFUIUGUJUK $.
    $( [8-Jun-2013] $)


