$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +c $. $( Cardinal number addition $)

  $( Extend class definition to include cardinal number addition. $)
  ccda $a class +c $.

  ${
    $d x y z $.
    $( Define cardinal number addition.  Definition of cardinal sum in
       [Mendelson] p. 258.  See ~ cdaval for its value and a description. $)
    df-cda $a |- +c = { <. <. x , y >. , z >. |
                   z = ( ( x X. { (/) } ) u. ( y X. { 1o } ) ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258. $)
    cdavalt $p |- ( ( A e. C /\ B e. D ) ->  ( A +c B ) =
                    ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) $=
      ( vx vy vz cvv wcel ccda co c0 csn cxp c1o cun wceq wa p0ex xpexg mpan2
      snex anim12i unexb sylib cv xpeq1 uneq1d uneq2d copab2 df-cda visset
      pm3.2i biantrur oprabbii eqtr oprabval2g 3expa mpdan elisset syl2an ) AHI
      ZBHIZABJKALMZNZBOMZNZPZQZACIBDIVBVCRZVHHIZVIVJVEHIZVGHIZRVKVBVLVCVMVBVDHI
      VLSAVDHHTUAVCVFHIVMOUBBVFHHTUAUCVEVGUDUEVBVCVKVIEFGABHHEUFZVDNZFUFZVFNZPZ
      VHJVEVQPHVNAQVOVEVQVNAVDUGUHVPBQVQVGVEVPBVFUGUIJGUFVRQZEFGUJVNHIZVPHIZRZV
      SRZEFGUJEFGUKVSWCEFGWBVSVTWAEULFULUMUNUOUPUQURUSACUTBDUTVA $.
      $( [28-Sep-2004] $) $( [24-Sep-2004] $)
  $}

  ${
    cdaval.1 $e |- A e. V $.
    cdaval.2 $e |- B e. V $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258.  For cardinal arithmetic, we follow Mendelson.  Rather than
       defining operations restricted to cardinal numbers, we use this disjoint
       union operation for addition, while cross product and set exponentiation
       stand in for cardinal multiplication and exponentiation.  Equinumerosity
       and dominance serve the roles of equality and ordering.  If we wanted
       to, we could easily convert our theorems to actual cardinal number
       operations via ~ carden , ~ carddom , and ~ cardsdom . The advantage of
       Mendelson's approach is that we can directly use many equinumerosity
       theorems that we already have available.  $)
    cdaval $p |- ( A +c B ) = ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) $=
      ( cvv wcel ccda co c0 csn cxp c1o cun wceq cdavalt mp2an ) AEFBEFABGHAIJK
      BLJKMNCDABEEOP $.
      $( [28-Sep-2004] $) $( [27-Sep-2004] $)

    $( Cardinal addition dominates union. $)
    uncdadom $p |- ( A u. B ) ~<_ ( A +c B ) $=
      ( cun c0 csn cxp c1o ccda co cdom wbr wa cin wceq cen 0ex xpsnen ensymi
      endom ax-mp con0 1on elisseti pm3.2i wn 1ne0OLD eqcom mtbir xpsndisj
      p0ex xpex snex undom mp2an cdaval breqtrr ) ABEZAFGZHZBIGZHZEZABJKLAVALMZ
      BVCLMZNVAVCOFPZUSVDLMVEVFAVAQMVEVAACAFCRSTAVAUAUBBVCQMVFVCBDBIDIUCUDUESTB
      VCUAUBUFFIPZUGVGVHIFPUHFIUIUJAFBIUKUBAVABVCAUTCULUMDBVBDIUNUMUOUPABCDUQUR
      $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( Cardinal addition is equinumerous to union for disjoint sets. $)
    cdaun $p |- ( ( A i^i B ) = (/) -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( cin c0 wceq csn cxp c1o cun ccda co cen wbr wn 1ne0OLD eqcom mtbir
      xpsndisj ax-mp wa 0ex xpsnen con0 1on elisseti unen mpanl12 mpan cdaval
      syl5eqbr ) ABEFGZAFHIZBJHIZKZABKZABLMNUNUOEFGZUMUPUQNOZFJGZPURUTJFGQFJRSA
      FBJTUAUNANOUOBNOURUMUBUSAFCUCUDBJDJUEUFUGUDUNAUOBUHUIUJABCDUKUL $.
      $( [?] $) $( [5-Apr-2007] $)
  $}

  $( 1+1=2 for cardinal number addition.  Theorem *110.643 of _Principia
     Mathematica_, vol. II, p. 86.  Unlike us, they define cardinal addition on
     collections of all sets equinumerous to 1 and 2 (which for us are proper
     classes unless we restrict them as in ~ karden ), but after applying
     definitions, our theorem is equivalent.   See also the comment for
     ~ pm54.43 .   The comment for ~ cdaval explains why we use ` ~~ ` instead
     of =. $)
  1p1e2cda $p |- ( 1o +c 1o ) ~~ 2o $=
    ( c1o ccda co c0 csn cxp cun c2o cen con0 1on elisseti cdaval cin wceq wbr
    wn 1ne0OLD eqcom mtbir xpsndisj ax-mp wb 0ex xpsnen pm54.43 mp2an mpbi
    eqbrtr ) AABCADEFZAAEFZGZHIAAAJKLZUMMUJUKNDOZULHIPZDAOZQUNUPADORDASTADAAUAU
    BUJAIPUKAIPUNUOUCADUMUDUEAAUMUMUEUJUKUFUGUHUI $.
    $( [5-Apr-2007] $) $( [5-Apr-2007] $)

  ${
    cdaen.1 $e |- A e. V $.
    cdaen.2 $e |- B e. V $.
    cdaen.3 $e |- C e. V $.
    cdaen.4 $e |- D e. V $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of
       [Mendelson] p. 258. $)
    cdaen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( cen wbr wa c0 csn cxp c1o cun ccda co cin wceq wn 1ne0OLD eqcom mtbir
      xpsndisj ax-mp pm3.2i unen mpan2 cvv wcel wb 0ex xpsnen enen1 mp2an
      enen2 bitr con0 1on elisseti syl2anbr cdaval 3brtr4g ) ABIJZCDIJZKALMZNZC
      OMZNZPZBVGNZDVINZPZACQRBDQRIVHVLIJZVJVMIJZVKVNIJZVEVFVOVPKVHVJSLTZVLVMSLT
      ZKVQVRVSLOTZUAZVRVTOLTUBLOUCUDZALCOUEUFWAVSWBBLDOUEUFUGVHVLVJVMUHUIVOAVLI
      JZVEAUJUKVHAIJVOWCULEALEUMUNVHAVLUJUOUPBUJUKVLBIJWCVEULFBLFUMUNVLBAUJUQUP
      URVPCVMIJZVFCUJUKVJCIJVPWDULGCOGOUSUTVAZUNVJCVMUJUOUPDUJUKVMDIJWDVFULHDOH
      WEUNVMDCUJUQUPURVBACEGVCBDFHVCVD $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)
  $}

  ${
    cda0en.1 $e |- A e. V $.
    $( Cardinal addition with cardinal zero (the empty set).  Part (a1) of
       proof of Theorem 6J of [Enderton] p. 143. $)
    cda0en $p |- ( A +c (/) ) ~~ A $=
      ( c0 ccda co csn cxp cen c1o cun 0ex cdaval xp0r uneq2i un0 3eqtr xpsnen
      eqbrtr ) ACDEZACFGZAHSTCIFZGZJTCJTACBKLUBCTUAMNTOPACBKQR $.
      $( [29-Sep-2004] $) $( [27-Sep-2004] $)

    $( Cardinal addition with cardinal one (which is the same as ordinal one).
       Used in proof of Theorem 6J of [Enderton] p. 143. $)
    cda1en $p |- ( A +c 1o ) ~~ suc ( card ` A ) $=
      ( c0 csn cxp c1o cun ccrd cfv ccda co csuc cen wbr wa cin wceq 0ex
      xpsnen cardid entr4 con0 1on elisseti fvex ensn1 pm3.2i wn 1ne0OLD eqcom
      mtbir xpsndisj ax-mp word cardon onord orddisj unen mp2an cdaval df-suc
      3brtr4 ) ACDEZFFDEZGZAHIZVFDZGZAFJKVFLMVCVFMNZVDVGMNZOVCVDPCQZVFVGPCQZOVE
      VHMNVIVJVCAVFBACBRSATUAVDFVGFUBUCUDZFFVMVMSVFAHUEUFUAUGVKVLCFQZUHVKVNFCQU
      ICFUJUKACFFULUMVFUNVLVFAUOUPVFUQUMUGVCVFVDVGURUSAFBVMUTVFVAVB $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( One times a cardinal number. $)
    xp1en $p |- ( A X. 1o ) ~~ A $=
      ( c1o cxp c0 csn cen wceq df1o2 xpeq2 ax-mp 0ex xpsnen eqbrtr ) ACDZAEFZD
      ZAGCPHOQHICPAJKAEBLMN $.
      $( [29-Sep-2004] $) $( [27-Sep-2004] $)

    $( Two times a cardinal number.  Exercise 4.56(g) of [Mendelson] p. 258. $)
    xp2cda $p |- ( A X. 2o ) = ( A +c A ) $=
      ( c0 csn c1o cun cxp c2o ccda co xpundi wceq cpr df-pr df2o2 df1o2 sneqi
      uneq2i 3eqtr4 xpeq2 ax-mp cdaval ) ACDZEDZFZGZAUCGAUDGFAHGZAAIJAUCUDKHUEL
      UGUFLCUCMUCUCDZFHUECUCNOUDUHUCEUCPQRSHUEATUAAABBUBS $.
      $( [29-Sep-2004] $) $( [27-Sep-2004] $)
  $}

  ${
    cdacomen.1 $e |- A e. V $.
    cdacomen.2 $e |- B e. V $.
    $( Commutative law for cardinal addition.  Exercise 4.56(c) of [Mendelson]
       p. 258. $)
    cdacomen $p |- ( A +c B ) ~~ ( B +c A ) $=
      ( c0 csn cxp c1o cun ccda co cen cin wceq wbr wn 1ne0OLD eqcom mtbir
      xpsndisj ax-mp wa 0ex xpsnen con0 1on elisseti entr4 unen mpanl12 mp2an
      uncom breqtr cdaval 3brtr4 ) AEFZGZBHFZGZIZBUPGZAURGZIZABJKBAJKLUTVBVAIZV
      CLUQUSMENZVBVAMENZUTVDLOZEHNZPVEVHHENZQEHRSAEBHTUAVIPVFQAHBETUAUQVBLOUSVA
      LOVEVFUBVGUQAVBCAECUCUDAHCHUEUFUGZUDUHUSBVADBHDVJUDBEDUCUDUHUQVBUSVAUIUJU
      KVBVAULUMABCDUNBADCUNUO $.
      $( [24-Jul-2006] $) $( [24-Sep-2004] $)

    ${
      cdaassen.3 $e |- C e. V $.
      $( Associative law for cardinal addition.  Exercise 4.56(c) of
         [Mendelson] p. 258. $)
      cdaassen $p |- ( ( A +c B ) +c C ) ~~ ( A +c ( B +c C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen wbr wa cin wceq cvv wcel p0ex xpex
        snex unex xpundir eqeng mp2 con0 1on elisseti xpsnen ensymi pm3.2i wn
        1ne0OLD eqcom mtbir xpsndisj ax-mp undisj1 mpbi unen mp2an enref
        xpassen xpcomen xpen entr4 entr xpeq1 xpindir xp0r 3eqtr3 unass breqtr
        0ex undisj2 cdaval opreq1i eqtr opreq2i 3brtr4 ) AGHZIZBJHZIZKZWCIZCWEI
        ZKZWDBWCIZWIKZWEIZKZABLMZCLMZABCLMZLMZNWJWDWCIZWFWCIZKZWIWEIZKZWNWHXANO
        ZWIXBNOZPWHWIQGRZXAXBQGRZPWJXCNOXDXEWHSTWHXARXDWGWCWDWFAWCDUAUBZBWEEJUC
        ZUBUDZUAUBWDWFWCUEWHXASUFUGXBWICWEFXIUBZWIJXKJUHUIUJUKULUMXFXGGJRZUNZXF
        XLJGRUOGJUPUQZWGGCJURUSWSXBQGRZWTXBQGRZPXGXOXPXMXOXNWDGWIJURUSZXMXPXNWF
        GWIJURUSUMWSWTXBUTVAZUMWHXAWIXBVBVCXCWSWKWEIZXBKZKZWNWSXTWDWCXHUAUBZXSX
        BWKWEBWCEUAUBZXIUBWIWEXKXIUBZUDUDXCWSXSKZXBKZYANXAYENOZXBXBNOZPXGYEXBQG
        RZPXCYFNOYGYHWSWSNOZWTXSNOZPWSWTQZGRZWSXSQGRZPYGYJYKWSYBVDWTBWEWCIZIZXS
        BWEWCEXIUAVEYPBWCWEIZIZXSBYQEWCWEUAXIUBZUBBBNOYOYQNOYPYRNOBEVDWEWCXIUAV
        FBBYOYQEEWEWCXIUAUBYSVGVCBWCWEEUAXIVEVHVIUMYMYNWDWFQZWCIZGWCIZYLGYTGRZU
        UAUUBRXMUUCXNAGBJURUSYTGWCVJUSWDWFWCVKWCVLVMXMYNXNWDGWKJURUSZUMWSWSWTXS
        VBVCXBYDVDUMXGYIXRXOXSXBQZGRZPYIXOUUFXQWKWIQZWEIZGWEIZUUEGUUGGRZUUHUUIR
        XMUUJXNBGCJURUSUUGGWEVJUSWKWIWEVKWEVLVMUMWSXSXBUTVAUMXAYEXBXBVBVCWSXSXB
        VNVOWDWSNOZWMXTNOZPWDWMQGRZWSXTQGRZPWNYANOUUKUULWSWDXHWDGXHVPUKULWMSTWM
        XTRUULWLWEWKWIYCXKUDZXIUBWKWIWEUEWMXTSUFUGUMUUMUUNXMUUMXNAGWLJURUSYNXOP
        UUNYNXOUUDXQUMWSXSXBVQVAUMWDWSWMXTVBVCVHVIWPWGCLMWJWOWGCLABDEVRVSWGCXJF
        VRVTWRAWLLMWNWQWLALBCEFVRWAAWLDUUOVRVTWB $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Cardinal multiplication distributes over cardinal addition.  Theorem
         6I(3) of [Enderton] p. 142. $)
      xpcdaen $p |- ( A X. ( B +c C ) ) ~~ ( ( A X. B ) +c ( A X. C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen p0ex xpex snex unex wbr wa cin wceq
        xpassen pm3.2i wn 1ne0OLD eqcom mtbir xpsndisj ax-mp xpeq2 xpindi xp0
        3eqtr3 unen mp2an xpundi breqtrr ensymi cdaval 3brtr4 ) ABGHZIZCJHZIZKZ
        IZABIZVBIZACIZVDIZKZABCLMZIZVHVJLMNVLVGAVFDVCVEBVBEOPCVDFJQZPRPVLAVCIZA
        VEIZKZVGNVIVPNSZVKVQNSZTVIVKUAGUBZVPVQUAZGUBZTVLVRNSVSVTABVBDEOUCACVDDF
        VOUCUDWAWCGJUBZUEZWAWDJGUBUFGJUGUHZVHGVJJUIUJAVCVEUAZIZAGIZWBGWGGUBZWHW
        IUBWEWJWFBGCJUIUJWGGAUKUJAVCVEULAUMUNUDVIVPVKVQUOUPAVCVEUQURUSVMVFUBVNV
        GUBBCEFUTVMVFAUKUJVHVJABDEPACDFPUTVA $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Sum of exponents law for cardinal arithmetic.  Theorem 6I(4) of
         [Enderton] p. 142. $)
      mapcdaen $p |- ( A ^m ( B +c C ) ) ~~ ( ( A ^m B ) X. ( A ^m C ) ) $=
        ( ccda co cm c0 csn cxp c1o cun cen cdaval opreq2i cin wceq wbr wn
        1ne0OLD eqcom mtbir xpsndisj ax-mp p0ex xpex snex mapunen enref 0ex
        xpsnen mapen mp2an con0 1on elisseti oprex xpen entr eqbrtr ) ABCGHZIHA
        BJKZLZCMKZLZNZIHZABIHZACIHZLZOVCVHAIBCEFPQVIAVEIHZAVGIHZLZVLVEVGRJSZVIV
        OOTJMSZUAVPVQMJSUBJMUCUDBJCMUEUFVEVGABVDEUGUHZCVFFMUIUHZDUJUFVMVJOTZVNV
        KOTZVOVLOTAAOTZVEBOTVTADUKZBJEULUMAAVEBDDVREUNUOWBVGCOTWAWCCMFMUPUQURUM
        AAVGCDDVSFUNUOVMVJVNVKAVEIUSABIUSAVGIUSACIUSUTUOVAVB $.
        $( [24-Jul-2006] $) $( [27-Sep-2004] $)

      $( Ordering law for cardinal addition.  Exercise 4.56(f) of [Mendelson]
         p. 258. $)
      cdadom1 $p |- ( A ~<_ B -> ( A +c C ) ~<_ ( B +c C ) ) $=
        ( cdom wbr c0 csn cxp c1o cun ccda co cvv wcel cen wb 0ex xpsnen
        domen1 mp2an domen2 bitr snex xpex domrefg ax-mp wa cin wceq wn
        1ne0OLD eqcom mtbir xpsndisj p0ex undom mpan2 sylbir cdaval 3brtr4g )
        ABGHZAIJZKZCLJZKZMZBVEKZVHMZACNOBCNOGVDVFVJGHZVIVKGHZVLAVJGHZVDAPQVFARH
        VLVNSDAIDTUAVFAVJPUBUCBPQVJBRHVNVDSEBIETUAVJBAPUDUCUEVLVHVHGHZVMVHPQVOC
        VGFLUFUGZVHPUHUIVLVOUJVJVHUKIULZVMILULZUMVQVRLIULUNILUOUPBICLUQUIVFVJVH
        VHBVEEURUGVPVPUSUTUTVAACDFVBBCEFVBVC $.
        $( [24-Jul-2006] $) $( [28-Sep-2004] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdadom2 $p |- ( A ~<_ B -> ( C +c A ) ~<_ ( C +c B ) ) $=
        ( cdom wbr ccda co cdadom1 cen cdacomen endomtr mpan domentr mpan2
        3syl ) ABGHACIJZBCIJZGHZCAIJZTGHZUBCBIJZGHZABCDEFKUBSLHUAUCCAFDMUBSTNOU
        CTUDLHUEBCEFMUBTUDPQR $.
        $( [7-Oct-2004] $) $( [28-Sep-2004] $)
    $}

    $( A set is dominated by its cardinal sum with another. $)
    cdadom3 $p |- A ~<_ ( A +c B ) $=
      ( cun cdom wbr ccda co cvv wcel wss ssun1 ssdomg mp2 uncdadom domtr
      mp2an ) AABEZFGZSABHIZFGAUAFGAJKASLTCABMASJNOABCDPASUAQR $.
      $( [7-Oct-2004] $) $( [28-Sep-2004] $)
  $}

  $( The cardinal sum of two finite sets is finite. $)
  cdafi $p |- ( ( A ~< om /\ B ~< om ) -> ( A +c B ) ~< om ) $=
    ( com csdm wbr wa ccda co c0 csn cxp c1o cun cvv wcel wceq cdavalt sdomex
    pm3.26d syl2an unfi2 cen wb sdomen1 con0 0elon xpsneng mpan2 syl sylanc
    ibir 1on eqbrtrd ) ACDEZBCDEZFABGHZAIJKZBLJKZMZCDANOZBNOZUPUSPUNUOABNNQUNUT
    CNOZACRSZUOVAVBBCRSZTUQCDEZURCDEZUSCDEUNUOUQURUAUNVEUTUQAUBEZVEUNUCUNUQACNU
    DVCUNUTVGVCUTIUEOVGUFAINUEUGUHUIUJUKUOVFVAURBUBEZVFUOUCUOURBCNUDVDUOVAVHVDV
    ALUEOVHULBLNUEUGUHUIUJUKTUM $.
    $( [23-Oct-2004] $) $( [22-Oct-2004] $)

  ${
    cdainf.1 $e |- A e. V $.
    $( A set is infinite iff the cardinal sum with itself is infinite. $)
    cdainf $p |- ( om ~<_ A <-> om ~<_ ( A +c A ) ) $=
      ( com cdom wbr ccda co cdadom3 domtr mpan2 csdm wn cdafi anidms con3i
      cvv wcel wb omex oprex domtri mp2an 3imtr4 impbi ) CADEZCAAFGZDEZUEAUFDEU
      GAABBHCAUFIJUFCKEZLZACKEZLZUGUEUJUHUJUHAAMNOCPQZUFPQUGUIRSAAFTCUFPPUAUBUL
      APQUEUKRSBCAPPUAUBUCUD $.
      $( [23-Oct-2004] $) $( [22-Oct-2004] $)
  $}

