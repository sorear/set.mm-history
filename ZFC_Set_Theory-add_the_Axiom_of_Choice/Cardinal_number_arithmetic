$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
         Cardinal number arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +c $. $( Cardinal number addition $)

  $( Extend class definition to include cardinal number addition. $)
  ccda $a class +c $.

  ${
    $d x y z $.
    $( Define cardinal number addition.  Definition of cardinal sum in
       [Mendelson] p. 258.  See ~ cdavali for its value and a description. $)
    df-cda $a |- +c = { <. <. x , y >. , z >. |
                   z = ( ( x X. { (/) } ) u. ( y X. { 1o } ) ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258. $)
    cdaval $p |- ( ( A e. C /\ B e. D ) -> ( A +c B ) =
                    ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) ) $=
      ( vx vy vz cvv wcel ccda co c0 csn cxp c1o cun wceq wa p0ex xpexg mpan2
      snex anim12i unexb sylib cv xpeq1 uneq1d uneq2d copab2 df-cda visset
      pm3.2i biantrur oprabbii eqtri oprabval2g mpd3an3 elisset syl2an ) AHIZBH
      IZABJKALMZNZBOMZNZPZQZACIBDIVAVBVGHIZVHVAVBRVDHIZVFHIZRVIVAVJVBVKVAVCHIVJ
      SAVCHHTUAVBVEHIVKOUBBVEHHTUAUCVDVFUDUEEFGABHHEUFZVCNZFUFZVENZPZVGJVDVOPHV
      LAQVMVDVOVLAVCUGUHVNBQVOVFVDVNBVEUGUIJGUFVPQZEFGUJVLHIZVNHIZRZVQRZEFGUJEF
      GUKVQWAEFGVTVQVRVSEULFULUMUNUOUPUQURACUSBDUSUT $.
      $( [24-Sep-2004] $)
  $}

  ${
    cdaval.1 $e |- A e. _V $.
    cdaval.2 $e |- B e. _V $.
    $( Value of cardinal addition.  Definition of cardinal sum in [Mendelson]
       p. 258.  For cardinal arithmetic, we follow Mendelson.  Rather than
       defining operations restricted to cardinal numbers, we use this disjoint
       union operation for addition, while cross product and set exponentiation
       stand in for cardinal multiplication and exponentiation.  Equinumerosity
       and dominance serve the roles of equality and ordering.  If we wanted
       to, we could easily convert our theorems to actual cardinal number
       operations via ~ carden , ~ carddom , and ~ cardsdom .  The advantage of
       Mendelson's approach is that we can directly use many equinumerosity
       theorems that we already have available. $)
    cdavali $p |- ( A +c B ) = ( ( A X. { (/) } ) u. ( B X. { 1o } ) ) $=
      ( cvv wcel ccda co c0 csn cxp c1o cun wceq cdaval mp2an ) AEFBEFABGHAIJKB
      LJKMNCDABEEOP $.
      $( [27-Sep-2004] $)

    $( Cardinal addition dominates union. $)
    uncdadom $p |- ( A u. B ) ~<_ ( A +c B ) $=
      ( cun c0 csn cxp c1o ccda co cdom wbr wa cin wceq cen 0ex xpsnen ensymi
      endom ax-mp con0 1on elisseti pm3.2i xp01disj p0ex xpex snex undom mp2an
      cdavali breqtrri ) ABEZAFGZHZBIGZHZEZABJKLAUQLMZBUSLMZNUQUSOFPUOUTLMVAVBA
      UQQMVAUQACAFCRSTAUQUAUBBUSQMVBUSBDBIDIUCUDUESTBUSUAUBUFABUGAUQBUSAUPCUHUI
      DBURDIUJUIUKULABCDUMUN $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( Cardinal addition is equinumerous to union for disjoint sets. $)
    cdaun $p |- ( ( A i^i B ) = (/) -> ( A +c B ) ~~ ( A u. B ) ) $=
      ( cin c0 wceq csn cxp c1o cun ccda co cen wbr xp01disj wa 0ex xpsnen
      con0 1on elisseti unen mpanl12 mpan cdavali syl5eqbr ) ABEFGZAFHIZBJHIZKZ
      ABKZABLMNUIUJEFGZUHUKULNOZABPUIANOUJBNOUMUHQUNAFCRSBJDJTUAUBSUIAUJBUCUDUE
      ABCDUFUG $.
      $( [5-Apr-2007] $)
  $}

  ${
    $d A x y $.  $d B y $.
    $( Cardinal addition is equinumerous to union for disjoint sets.
       (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaung $p |- ( ( A e. C /\ B e. D /\ ( A i^i B ) = (/) ) ->
                   ( A +c B ) ~~ ( A u. B ) ) $=
      ( vx vy wcel cin c0 wceq ccda co cun cen wbr cv wi ineq1 eqeq1d opreq1
      uneq1 breq12d imbi12d ineq2 opreq2 uneq2 visset cdaun vtocl2g 3impia ) AC
      GBDGABHZIJZABKLZABMZNOZEPZFPZHZIJZUPUQKLZUPUQMZNOZQAUQHZIJZAUQKLZAUQMZNOZ
      QULUOQEFABCDUPAJZUSVDVBVGVHURVCIUPAUQRSVHUTVEVAVFNUPAUQKTUPAUQUAUBUCUQBJZ
      VDULVGUOVIVCUKIUQBAUDSVIVEUMVFUNNUQBAKUEUQBAUFUBUCUPUQEUGFUGUHUIUJ $.
      $( [5-Jun-2009] $)
  $}

  $( 1+1=2 for cardinal number addition.  Theorem *110.643 of _Principia
     Mathematica_, vol.  II, p. 86, which adds the remark, "The above
     proposition is occasionally useful."  Unlike us, Whitehead and Russell
     define cardinal addition on collections of all sets equinumerous to 1 and
     2 (which for us are proper classes unless we restrict them as in
     ~ karden ), but after applying definitions, our theorem is equivalent.
     See also the comment for ~ pm54.43 .  The comment for ~ cdavali explains
     why we use ` ~~ ` instead of =. $)
  pm110.643 $p |- ( 1o +c 1o ) ~~ 2o $=
    ( c1o ccda co c0 csn cxp cun c2o cen con0 1on elisseti cdavali cin wceq
    wbr xp01disj wb 0ex xpsnen pm54.43 mp2an mpbi eqbrtri ) AABCADEFZAAEFZGZHIA
    AAJKLZUHMUEUFNDOZUGHIPZAAQUEAIPUFAIPUIUJRADUHSTAAUHUHTUEUFUAUBUCUD $.
    $( [5-Apr-2007] $)

  ${
    cdaen.1 $e |- A e. _V $.
    cdaen.2 $e |- B e. _V $.
    cdaen.3 $e |- C e. _V $.
    cdaen.4 $e |- D e. _V $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258. $)
    cdaen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( cen wbr wa c0 csn cxp c1o cun ccda co cin wceq xp01disj unen mpanr12
      cvv wcel wb 0ex xpsnen enen1 mp2an enen2 bitri con0 1on elisseti
      syl2anbr cdavali 3brtr4g ) ABIJZCDIJZKALMZNZCOMZNZPZBVANZDVCNZPZACQRBDQRI
      VBVFIJZVDVGIJZVEVHIJZUSUTVIVJKVBVDSLTVFVGSLTVKACUABDUAVBVFVDVGUBUCVIAVFIJ
      ZUSAUDUEVBAIJVIVLUFEALEUGUHVBAVFUDUIUJBUDUEVFBIJVLUSUFFBLFUGUHVFBAUDUKUJU
      LVJCVGIJZUTCUDUEVDCIJVJVMUFGCOGOUMUNUOZUHVDCVGUDUIUJDUDUEVGDIJVMUTUFHDOHV
      NUHVGDCUDUKUJULUPACEGUQBDFHUQUR $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)
  $}

  ${
    $d A w x y z $.  $d B w y z $.  $d C w z $.  $d D w $.  $d W w z $.
    $d X w z $.
    $( Cardinal addition of equinumerous sets.  Exercise 4.56(b) of [Mendelson]
       p. 258.  (Contributed by Paul Chapman, 5-Jun-2009.) $)
    cdaeng $p |- ( ( ( A e. W /\ B e. X ) /\ ( C e. Y /\ D e. Z ) /\
                     ( A ~~ B /\ C ~~ D ) ) -> ( A +c C ) ~~ ( B +c D ) ) $=
      ( vz vw vx vy wcel wa cen wbr ccda co wi cv wceq breq1 anbi2d opreq2
      breq1d imbi12d imbi2d breq2 breq2d anbi1d opreq1 visset cdaen vtocl2g
      impcom 3impia ) AEMBFMNZCGMDHMNZABOPZCDOPZNZACQRZBDQRZOPZURUQVAVDSZUQUSIT
      ZJTZOPZNZAVFQRZBVGQRZOPZSZSUQUSCVGOPZNZVBVKOPZSZSUQVESIJCDGHVFCUAZVMVQUQV
      RVIVOVLVPVRVHVNUSVFCVGOUBUCVRVJVBVKOVFCAQUDUEUFUGVGDUAZVQVEUQVSVOVAVPVDVS
      VNUTUSVGDCOUHUCVSVKVCVBOVGDBQUDUIUFUGKTZLTZOPZVHNZVTVFQRZWAVGQRZOPZSAWAOP
      ZVHNZVJWEOPZSVMKLABEFVTAUAZWCWHWFWIWJWBWGVHVTAWAOUBUJWJWDVJWEOVTAVFQUKUEU
      FWABUAZWHVIWIVLWKWGUSVHWABAOUHUJWKWEVKVJOWABVGQUKUIUFVTWAVFVGKULLULIULJUL
      UMUNUNUOUP $.
      $( [5-Jun-2009] $)
  $}

  ${
    cda0en.1 $e |- A e. _V $.
    $( Cardinal addition with cardinal zero (the empty set).  Part (a1) of
       proof of Theorem 6J of [Enderton] p. 143. $)
    cda0en $p |- ( A +c (/) ) ~~ A $=
      ( c0 ccda co csn cxp cen c1o cun 0ex cdavali xp0r uneq2i un0 3eqtri
      xpsnen eqbrtri ) ACDEZACFGZAHSTCIFZGZJTCJTACBKLUBCTUAMNTOPACBKQR $.
      $( [27-Sep-2004] $)

    $( Cardinal addition with cardinal one (which is the same as ordinal one).
       Used in proof of Theorem 6J of [Enderton] p. 143. $)
    cda1en $p |- ( A +c 1o ) ~~ suc ( card ` A ) $=
      ( c0 csn cxp c1o cun ccrd cfv ccda co csuc cen wbr wa cin wceq 0ex
      xpsnen cardid entr4i con0 1on elisseti fvex ensn1 pm3.2i xp01disj word
      cardon onordi orddisj ax-mp unen mp2an cdavali df-suc 3brtr4i ) ACDEZFFDE
      ZGZAHIZVBDZGZAFJKVBLMUSVBMNZUTVCMNZOUSUTPCQZVBVCPCQZOVAVDMNVEVFUSAVBBACBR
      SATUAUTFVCFUBUCUDZFFVIVISVBAHUEUFUAUGVGVHAFUHVBUIVHVBAUJUKVBULUMUGUSVBUTV
      CUNUOAFBVIUPVBUQUR $.
      $( [24-Jul-2006] $) $( [28-Sep-2004] $)

    $( One times a cardinal number. $)
    xp1en $p |- ( A X. 1o ) ~~ A $=
      ( c1o cxp c0 csn cen df1o2 xpeq2i 0ex xpsnen eqbrtri ) ACDAEFZDAGCMAHIAEB
      JKL $.
      $( [27-Sep-2004] $)

    $( Two times a cardinal number.  Exercise 4.56(g) of [Mendelson] p. 258. $)
    xp2cda $p |- ( A X. 2o ) = ( A +c A ) $=
      ( c0 csn c1o cun cxp c2o ccda co xpundi cpr df-pr df2o2 df1o2 sneqi
      uneq2i 3eqtr4i xpeq2i cdavali ) ACDZEDZFZGAUAGAUBGFAHGAAIJAUAUBKHUCACUALU
      AUADZFHUCCUAMNUBUDUAEUAOPQRSAABBTR $.
      $( [27-Sep-2004] $)
  $}

  ${
    cdacomen.1 $e |- A e. _V $.
    cdacomen.2 $e |- B e. _V $.
    $( Commutative law for cardinal addition.  Exercise 4.56(c) of [Mendelson]
       p. 258. $)
    cdacomen $p |- ( A +c B ) ~~ ( B +c A ) $=
      ( c0 csn cxp c1o cun ccda co cen cin wceq wbr xp01disj wne 1n0 xpsndisj
      ax-mp wa 0ex xpsnen con0 1on elisseti entr4i unen mpanl12 mp2an uncom
      breqtri cdavali 3brtr4i ) AEFZGZBHFZGZIZBUOGZAUQGZIZABJKBAJKLUSVAUTIZVBLU
      PURMENZVAUTMENZUSVCLOZABPHEQVERAHBESTUPVALOURUTLOVDVEUAVFUPAVACAECUBUCAHC
      HUDUEUFZUCUGURBUTDBHDVGUCBEDUBUCUGUPVAURUTUHUIUJVAUTUKULABCDUMBADCUMUN $.
      $( [24-Jul-2006] $) $( [24-Sep-2004] $)

    ${
      cdaassen.3 $e |- C e. _V $.
      $( Associative law for cardinal addition.  Exercise 4.56(c) of
         [Mendelson] p. 258. $)
      cdaassen $p |- ( ( A +c B ) +c C ) ~~ ( A +c ( B +c C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen wbr wa cin wceq cvv wcel p0ex xpex
        snex unex xpundir eqeng mp2 con0 1on elisseti xpsnen ensymi pm3.2i
        xp01disj undisj1 mpbi unen mp2an enref xpassen xpcomen xpen entr4i
        entri xpeq1i xpindir xp0r 3eqtr3i unass breqtri 0ex undisj2 cdavali
        opreq1i eqtri opreq2i 3brtr4i ) AGHZIZBJHZIZKZVRIZCVTIZKZVSBVRIZWDKZVTI
        ZKZABLMZCLMZABCLMZLMZNWEVSVRIZWAVRIZKZWDVTIZKZWIWCWPNOZWDWQNOZPWCWDQGRZ
        WPWQQGRZPWEWRNOWSWTWCSTWCWPRWSWBVRVSWAAVRDUAUBZBVTEJUCZUBUDZUAUBVSWAVRU
        EWCWPSUFUGWQWDCVTFXDUBZWDJXFJUHUIUJUKULUMXAXBWBCUNWNWQQGRZWOWQQGRZPXBXG
        XHVSWDUNZWAWDUNUMWNWOWQUOUPZUMWCWPWDWQUQURWRWNWFVTIZWQKZKZWIWNXLVSVRXCU
        AUBZXKWQWFVTBVREUAUBZXDUBWDVTXFXDUBZUDUDWRWNXKKZWQKZXMNWPXQNOZWQWQNOZPX
        BXQWQQGRZPWRXRNOXSXTWNWNNOZWOXKNOZPWNWOQZGRZWNXKQGRZPXSYBYCWNXNUSWOBVTV
        RIZIZXKBVTVREXDUAUTYHBVRVTIZIZXKBYIEVRVTUAXDUBZUBBBNOYGYINOYHYJNOBEUSVT
        VRXDUAVABBYGYIEEVTVRXDUAUBYKVBURBVRVTEUAXDUTVCVDUMYEYFVSWAQZVRIGVRIYDGY
        LGVRABUNVEVSWAVRVFVRVGVHVSWFUNZUMWNWNWOXKUQURWQXPUSUMXBYAXJXGXKWQQZGRZP
        YAXGYOXIWFWDQZVTIGVTIYNGYPGVTBCUNVEWFWDVTVFVTVGVHUMWNXKWQUOUPUMWPXQWQWQ
        UQURWNXKWQVIVJVSWNNOZWHXLNOZPVSWHQGRZWNXLQGRZPWIXMNOYQYRWNVSXCVSGXCVKUK
        ULWHSTWHXLRYRWGVTWFWDXOXFUDZXDUBWFWDVTUEWHXLSUFUGUMYSYTAWGUNYFXGPYTYFXG
        YMXIUMWNXKWQVLUPUMVSWNWHXLUQURVCVDWKWBCLMWEWJWBCLABDEVMVNWBCXEFVMVOWMAW
        GLMWIWLWGALBCEFVMVPAWGDUUAVMVOVQ $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Cardinal multiplication distributes over cardinal addition.  Theorem
         6I(3) of [Enderton] p. 142. $)
      xpcdaen $p |- ( A X. ( B +c C ) ) ~~ ( ( A X. B ) +c ( A X. C ) ) $=
        ( c0 csn cxp c1o cun ccda co cen p0ex xpex snex unex wbr wa cin wceq
        xpassen pm3.2i xp01disj xpeq2i xpindi xp0 3eqtr3i unen mp2an xpundi
        breqtrri ensymi cdavali 3brtr4i ) ABGHZIZCJHZIZKZIZABIZUQIZACIZUSIZKZAB
        CLMZIVCVELMNVGVBAVADURUTBUQEOPCUSFJQZPRPVGAURIZAUTIZKZVBNVDVJNSZVFVKNSZ
        TVDVFUAGUBZVJVKUAZGUBZTVGVLNSVMVNABUQDEOUCACUSDFVIUCUDVOVQVCVEUEAURUTUA
        ZIAGIVPGVRGABCUEUFAURUTUGAUHUIUDVDVJVFVKUJUKAURUTULUMUNVHVAABCEFUOUFVCV
        EABDEPACDFPUOUP $.
        $( [24-Jul-2006] $) $( [26-Sep-2004] $)

      $( Sum of exponents law for cardinal arithmetic.  Theorem 6I(4) of
         [Enderton] p. 142. $)
      mapcdaen $p |- ( A ^m ( B +c C ) ) ~~ ( ( A ^m B ) X. ( A ^m C ) ) $=
        ( ccda co cmap c0 csn cxp c1o cun cen cdavali opreq2i cin wceq wbr
        xp01disj p0ex xpex snex mapunen ax-mp enref 0ex xpsnen mapen mp2an
        con0 1on elisseti oprex xpen entri eqbrtri ) ABCGHZIHABJKZLZCMKZLZNZIHZ
        ABIHZACIHZLZOUSVDAIBCEFPQVEAVAIHZAVCIHZLZVHVAVCRJSVEVKOTBCUAVAVCABUTEUB
        UCZCVBFMUDUCZDUEUFVIVFOTZVJVGOTZVKVHOTAAOTZVABOTVNADUGZBJEUHUIAAVABDDVL
        EUJUKVPVCCOTVOVQCMFMULUMUNUIAAVCCDDVMFUJUKVIVFVJVGAVAIUOABIUOAVCIUOACIU
        OUPUKUQUR $.
        $( [24-Jul-2006] $) $( [27-Sep-2004] $)

      $( Ordering law for cardinal addition.  Exercise 4.56(f) of [Mendelson]
         p. 258. $)
      cdadom1 $p |- ( A ~<_ B -> ( A +c C ) ~<_ ( B +c C ) ) $=
        ( cdom wbr c0 csn cxp c1o cun ccda co cvv wcel cen wb 0ex xpsnen
        domen1 mp2an domen2 bitri snex xpex domrefg ax-mp wa cin wceq xp01disj
        p0ex undom mpan2 sylbir cdavali 3brtr4g ) ABGHZAIJZKZCLJZKZMZBVAKZVDMZA
        CNOBCNOGUTVBVFGHZVEVGGHZVHAVFGHZUTAPQVBARHVHVJSDAIDTUAVBAVFPUBUCBPQVFBR
        HVJUTSEBIETUAVFBAPUDUCUEVHVDVDGHZVIVDPQVKCVCFLUFUGZVDPUHUIVHVKUJVFVDUKI
        ULVIBCUMVBVFVDVDBVAEUNUGVLVLUOUPUPUQACDFURBCEFURUS $.
        $( [24-Jul-2006] $) $( [28-Sep-2004] $)

      $( Ordering law for cardinal addition.  Theorem 6L(a) of [Enderton]
         p. 149. $)
      cdadom2 $p |- ( A ~<_ B -> ( C +c A ) ~<_ ( C +c B ) ) $=
        ( cdom wbr ccda co cdadom1 cen cdacomen endomtr mpan domentr mpan2
        3syl ) ABGHACIJZBCIJZGHZCAIJZTGHZUBCBIJZGHZABCDEFKUBSLHUAUCCAFDMUBSTNOU
        CTUDLHUEBCEFMUBTUDPQR $.
        $( [28-Sep-2004] $)
    $}

    $( A set is dominated by its cardinal sum with another. $)
    cdadom3 $p |- A ~<_ ( A +c B ) $=
      ( cun cdom wbr ccda co cvv wcel wss ssun1 ssdomg mp2 uncdadom domtr
      mp2an ) AABEZFGZSABHIZFGAUAFGAJKASLTCABMASJNOABCDPASUAQR $.
      $( [28-Sep-2004] $)
  $}

  $( The cardinal sum of two finite sets is finite. $)
  cdafi $p |- ( ( A ~< om /\ B ~< om ) -> ( A +c B ) ~< om ) $=
    ( com csdm wbr wa ccda co c0 csn cxp c1o cun cvv wcel wceq cdaval sdomex
    simpld syl2an unfi2 cen wb con0 xpsneng 0elon sylancl sdomen1 syl11anc
    ibir 1on eqbrtrd ) ACDEZBCDEZFABGHZAIJKZBLJKZMZCDANOZBNOZUOURPUMUNABNNQUMUS
    CNOZACRSZUNUTVABCRSZTUPCDEZUQCDEZURCDEUMUNUPUQUAUMVDUMUSUPAUBEZVDUMUCVBUSIU
    DOVFUMAINUDUEVBUFUGUPACNUHUIUJUNVEUNUTUQBUBEZVEUNUCVCUTLUDOVGUNBLNUDUEVCUKU
    GUQBCNUHUIUJTUL $.
    $( [22-Oct-2004] $)

  ${
    cdainf.1 $e |- A e. _V $.
    $( A set is infinite iff the cardinal sum with itself is infinite. $)
    cdainf $p |- ( om ~<_ A <-> om ~<_ ( A +c A ) ) $=
      ( com cdom wbr ccda co cdadom3 domtr mpan2 csdm wn cdafi anidms con3i
      cvv wcel wb omex oprex domtri mp2an 3imtr4i impbii ) CADEZCAAFGZDEZUEAUFD
      EUGAABBHCAUFIJUFCKEZLZACKEZLZUGUEUJUHUJUHAAMNOCPQZUFPQUGUIRSAAFTCUFPPUAUB
      ULAPQUEUKRSBCAPPUAUBUCUD $.
      $( [22-Oct-2004] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d x y z $.
    $( The cardinal and ordinal sums of finite ordinals are equal.
       (Contributed by Paul Chapman, 11-Apr-2009.) $)
    nnacda $p |- ( ( A e. om /\ B e. om ) ->
                   ( card ` ( A +c B ) ) = ( A +o B ) ) $=
      ( vx vy vz com wcel wa cv ccda co ccrd cfv coa wceq wral c0 csuc opreq2
      fveq2d eqeq12d cardnn cen wbr visset cda0en cvv wb oprex carden mp2an
      mpbir syl5eq nna0 eqtr4d wi w3a c1o cda1en suceq syl breq2d mpbii enref
      sucex cdaen mpan 1onn elisseti cdaassen entr 3syl ensym mpan2 3ad2ant2
      3ad2ant3 nnasuc 3adant3 mpbid sylibr nnacl peano2b sylan2b eqtrd 3expia
      expcom finds2 impcom rgen2a opreq1 rcla42v mpi ) AFGBFGHCIZDIZJKZLMZWMWNN
      KZOZDFPCFPABJKZLMZABNKZOZWRCDFWNFGWMFGZWRWRWMQJKZLMZWMQNKZOWMEIZJKZLMZWMX
      GNKZOZWMXGRZJKZLMZWMXLNKZOZXCDEWNQOZWPXEWQXFXQWOXDLWNQWMJSTWNQWMNSUAWNXGO
      ZWPXIWQXJXRWOXHLWNXGWMJSTWNXGWMNSUAWNXLOZWPXNWQXOXSWOXMLWNXLWMJSTWNXLWMNS
      UAXCXEWMXFXCWMLMZWMXEWMUBXEXTOZXDWMUCUDZWMCUEZUFXDUGGWMUGGYAYBUHWMQJUIYCX
      DWMUGUGUJUKULUMWMUNUOXCXGFGZXKXPUPXCYDXKXPXCYDXKUQZXNXOLMZXOYEXMXOUCUDZXN
      YFOZYEXMXIRZUCUDZYGYDXCYJXKYDXHURJKZXMUCUDZXMYKUCUDZYJYDXGURJKZXLUCUDZWMY
      NJKZXMUCUDZYLYDYNXGLMZRZUCUDYOXGEUEZUSYDYSXLYNUCYDYRXGOYSXLOXGUBYRXGUTVAV
      BVCWMWMUCUDYOYQWMYCVDWMWMYNXLYCYCXGURJUIXGYTVEVFVGYKYPUCUDYQYLWMXGURYCYTU
      RFVHVIVJYKYPXMVKVGVLYKXMWMXLJUIZVMYMYKYIUCUDYJXHWMXGJUIUSXMYKYIVKVNVLVOYE
      YIXOXMUCYEYIXJRZXOXKXCYIUUBOYDXIXJUTVPXCYDXOUUBOXKWMXGVQVRUOVBVSXMUGGXOUG
      GYHYGUHUUAWMXLNUIXMXOUGUGUJUKVTXCYDYFXOOZXKXCYDHXOFGZUUCXCXLFGUUDYDWMXLWA
      XGWBWCXOUBVAVRWDWEWFWGWHWIWRXBAWNJKZLMZAWNNKZOCDABFFWMAOZWPUUFWQUUGUUHWOU
      UELWMAWNJWJTWMAWNNWJUAWNBOZUUFWTUUGXAUUIUUEWSLWNBAJSTWNBANSUAWKWL $.
      $( [11-Apr-2009] $)
  $}

  $( The cardinality of the union of disjoint, finite sets is the ordinal sum
     of their cardinalities.  (Contributed by Paul Chapman, 5-Jun-2009.) $)
  nnaun $p |- ( ( A e. Fin /\ B e. Fin /\ ( A i^i B ) = (/) ) ->
                 ( card ` ( A u. B ) ) = ( ( card ` A ) +o ( card ` B ) ) ) $=
    ( cfn wcel cin c0 wceq w3a ccrd cfv coa co cun cen wbr ccda entr cardid
    cvv wa fvex pm3.2i an4 cdaeng mp3an3 sylbi mpan 3adant3 cdaung syl11anc
    sylancr wb com nnacda ficardom syl2an breq1d mpbid carden oprex unfi
    mpbird nnacl cardnn syl eqtr3d ) ACDZBCDZABEFGZHZAIJZBIJZKLZIJZABMZIJZVMVJV
    NVPGZVMVONOZVJVKVLPLZIJZVONOZVRVTVSNOVSVONOZWAVJVTVSVOQVSRVJVSABPLZNOZWCVON
    OWBVGVHWDVIVKSDZVLSDZTZVGVHTZWDWEWFAIUABIUAUBWGWHTWEVGTZWFVHTZTWDWEWFVGVHUC
    WIWJVKANOZVLBNOZTWDWKWLARBRUBVKAVLBSCSCUDUEUFUGUHABCCUIVSWCVOQUJUKVGVHWAVRU
    LVIWHVTVMVONVKUMDZVLUMDZVTVMGVGVHVKVLUNAUOZBUOZUPUQUHURVGVHVQVRULZVIVMSDVOC
    DWQWHVMVOSCUSVKVLKUTABVAUKUHVBVGVHVNVMGZVIWMWNWRVGVHWMWNTVMUMDWRVKVLVCVMVDV
    EWOWPUPUHVF $.
    $( [5-Jun-2009] $)


