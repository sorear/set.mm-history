$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
             Introduce the Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w v u t $.
    $( Axiom of Choice.  The Axiom of Choice (AC) is usually considered an
       extension of ZF set theory rather than a proper part of it.  It is
       sometimes considered philosophically controversial because it asserts
       the existence of a set without telling us what the set is.  ZF set
       theory that includes AC is called ZFC.

       The unpublished version given here says that given any set ` x ` , there
       exists a ` y ` that is a collection of unordered pairs, one pair for
       each non-empty member of ` x ` .  One entry in the pair is the member of
       ` x ` , and the other entry is some arbitrary member of that member of
       ` x ` .  See the rewritten version ~ ac3 for a more detailed
       explanation.

       This version was specifically crafted to be short when expanded to
       primitives.  Kurt Maes' 5-quantifier version ~ ackm is slightly shorter
       when the biconditional of ~ ax-ac is expanded into implication and
       negation.

       Standard textbook versions of AC are derived as ~ ac8 , ~ ac5 , and
       ~ ac7 .  The Axiom of Regularity ~ ax-reg (among others) is used to
       derive our version from the standard ones; this reverse derivation is
       shown as theorem ~ aceq6b .  Equivalents to AC are the well-ordering
       theorem ~ weth and Zorn's lemma ~ zorn .  See ~ ac4 for comments about
       stronger versions of AC. $)
    ax-ac $a |- E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) ) $.

    $( Axiom of Choice expressed with fewest number of different variables.
       The penultimate step shows the logical equivalence to ~ ax-ac . $)
    zfac $p |- E. x A. y A. z ( ( y e. z /\ z e. w ) -> E. w A. y ( E. w
              ( ( y e. z /\ z e. w ) /\ ( y e. w /\ w e. x ) ) <-> y = w ) ) $=
      ( vu vt vv cv wcel wa wex wceq wb wal wi ax-ac equequ2 bibi2d elequ2
      anbi2d elequ1 anbi12d cbvexv bibi1i syl6bb albidv anbi1d exbidv equequ1
      bibi12d cbvalv imbi2i 2albii exbii mpbi ) BHZCHZIZUQDHZIZJZEHZUQIZUQFHZIZ
      JZVBVDIZVDAHZIZJZJZFKZVBGHZLZMZENZGKZOZCNBNZAKVAVAUPUSIZUSVHIZJZJZDKZUPUS
      LZMZBNZDKZOZCNBNZAKDABCGEFPVSWJAVRWIBCVQWHVAVPWGGDVMUSLZVPVCUTJZVBUSIZWAJ
      ZJZDKZVBUSLZMZENWGWKVOWREWKVOVLWQMWRWKVNWQVLGDEQRVLWPWQVKWOFDVDUSLZVFWLVJ
      WNWSVEUTVCFDCSTWSVGWMVIWAFDESFDAUAUBUBUCUDUEUFWRWFEBVBUPLZWPWDWQWEWTWOWCD
      WTWLVAWNWBWTVCURUTEBCUAUGWTWMVTWAEBDUAUGUBUHEBDUIUJUKUEUCULUMUNUO $.
      $( [14-Aug-2003] $)

    $( Axiom of Choice equivalent.  By using restricted quantifiers, we can
       express the Axiom of Choice with a single explicit conjunction.  (If you
       want to figure it out, the rewritten equivalent ~ ac3 is easier to
       understand.)  Note: ~ aceq0 shows the logical equivalence to
       ~ ax-ac . $)
    ac2 $p |- E. y A. z e. x A. w e. z E! v e. z E. u e. y
              ( z e. u /\ v e. u ) $=
      ( vt cv wcel wa wrex wreu wral wex wceq wb wal wi ax-ac aceq0 mpbir ) CHZ
      FHZIEHZUCIJFBHZKEUBLDUBMCAHZMBNUBDHZIUGUFIJUCUGIUGGHZIJUCUHIUHUEIJJGNUCUD
      OPFQENRDQCQBNABCDEFGSABCDEFGTUA $.
      $( [18-Jul-1996] $)
  $}

  ${
    $d x y z w v u $.
    $( Axiom of Choice using abbreviations.  The logical equivalence to ~ ax-ac
       can be established by chaining ~ aceq0 and ~ aceq2 .  A standard
       textbook version of AC is derived from this one in ~ aceq6a , and this
       version of AC is derived from the textbook version in ~ aceq6b .

       The following sketch will help you understand this version of the
       axiom.  Given any set ` x ` , the axiom says that there exists a ` y `
       that is a collection of unordered pairs, one pair for each non-empty
       member of ` x ` .  One entry in the pair is the member of ` x ` , and
       the other entry is some arbitrary member of that member of ` x ` .
       Using the Axiom of Regularity, we can show that ` y ` is really a set of
       _ordered_ pairs, very similar to the ordered pair construction
       ~ opthreg .  The key theorem for this (used in the proof of ~ aceq6b )
       is ~ preleq .  With this modified definition of ordered pair, it can be
       seen that ` y ` is actually a choice function on the members of ` x ` .

       For example, suppose ` x = { { 1 , 2 } , { 1 , 3 } , { 2 , 3 } } ` .
       Take
       ` y = { { { 1 , 2 } , 1 } , { { 1 , 3 } , 1 } , { { 2 , 3 } , 2 } } ` .
       For the member (of ` x ` ) ` z = { 1 , 2 } ` , the only assignment to
       ` w ` and ` v ` that satisfies the axiom is ` w = 1 ` and
       ` v = { { 1 , 2 } , 1 } ` , so there is exactly one ` w ` as required.
       We verify the other two members of ` x ` similarly.  Thus ` y `
       satisfies the axiom.  Using our modified ordered pair definition, it is
       easy to see that ` y ` is the choice function
       ` { <. { 1 , 2 } , 1 >. , <. { 1 , 3 } , 1 >. , `
       ` <. { 2 , 3 } , 2 >. } ` .  Of course other choices for ` y ` will also
       satisfy the axiom, for example
       ` y = { { { 1 , 2 } , 2 } , { { 1 , 3 } , 1 } , { { 2 , 3 } , 3 } } ` .
       What AC tells us is that there exists at least one such ` y ` , but it
       doesn't tell us which one. $)
    ac3 $p |- E. y A. z e. x ( z =/= (/) -> E! w e. z E. v e. y
              ( z e. v /\ w e. v ) ) $=
      ( vu cv wcel wa wrex wreu wral wex c0 wne wi ac2 aceq2 mpbi ) CGZFGZHEGZU
      AHIFBGZJETKDTLCAGZLBMTNOTUBHDGUBHIEUCJDTKPCUDLBMABCDEFQABCDEFRS $.
      $( [9-Apr-2007] $) $( [19-Jul-1996] $)
  $}

  ${
    $d f x y z w v u $.
    $( An Axiom of Choice equivalent similar to the Axiom of Choice (first
       form) of [Enderton] p. 49. $)
    ac7 $p |- E. f ( f C_ x /\ f Fn dom x ) $=
      ( vz vu vv vy vw cv wcel wa wrex wreu wral wex wss cdm wfn wal c0 wne wi
      aceq2 albii aceq6a sylbi 19.21bi ac2 mpg ) CHZDHZIEHZUJIJDFHZKEUILGUIMCAH
      ZMFNZBHZUMOUOUMPQJBNZAUNARZUPAUQUISTUIUKIGHUKIJEULKGUILUACUMMFNZARUPARUNU
      RAAFCGEDUBUCAFCGEBUDUEUFAFCGEDUGUH $.
      $( [7-Mar-2007] $) $( [29-Apr-2004] $)
  $}

  ${
    $d f x R $.
    $( An Axiom of Choice equivalent similar to the Axiom of Choice (first
       form) of [Enderton] p. 49. $)
    ac7g $p |- ( R e. A -> E. f ( f C_ R /\ f Fn dom R ) ) $=
      ( vx cv wss cdm wfn wa wex wceq sseq2 wb dmeq fneq2 syl anbi12d exbidv
      ac7 vtoclg ) CEZDEZFZUAUBGZHZIZCJUABFZUABGZHZIZCJDBAUBBKZUFUJCUKUCUGUEUIU
      BBUALUKUDUHKUEUIMUBBNUDUHUAOPQRDCST $.
      $( [30-Jul-2004] $) $( [23-Jul-2004] $)
  $}

  ${
    $d x z f $.
    $( Equivalent of Axiom of Choice.  We do not insist that ` f ` be a
       function.  However, theorem ~ ac5 , derived from this one, shows that
       this form of the axiom does imply that at least one such set ` f ` whose
       existence we assert is in fact a function.  Axiom of Choice of
       [TakeutiZaring] p. 83.

       Takeuti and Zaring call this "weak choice" in contrast to "strong
       choice" ` E. F A. z ( z =/= (/) -> ( F `` z ) e. z ) ` , which asserts
       the existence of a universal choice function but requires second-order
       quantification on (proper) class variable ` F ` and thus cannot be
       expressed in our first-order formalization.  However, it has been shown
       that ZF plus strong choice is a conservative extension of ZF plus weak
       choice.  See Ulrich Felgner, "Comparison of the axioms of local and
       universal choice," _Fundamenta Mathematica_, 71, 43-62 (1971).

       Weak choice can be strengthened in a different direction to choose from
       a collection of proper classes; see ~ ac6s5 . $)
    ac4 $p |- E. f A. z e. x ( z =/= (/) -> ( f ` z ) e. z ) $=
      ( cv c0 wne cfv wcel wi wral wex wss cdm wfn wa wal aceq3 ac7 mpgbi a4i
      ) BDZEFUACDZGUAHIBADZJCKZAUBUCLUBUCMNOCKUDAPAABCQACRST $.
      $( [30-Mar-2008] $) $( [21-Jul-1996] $)
  $}

  ${
    $d x y f A $.
    ac4c.1 $e |- A e. _V $.
    $( Equivalent of Axiom of Choice (class version) $)
    ac4c $p |- E. f A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) $=
      ( vy cv c0 wne cfv wcel wi wral wex wceq raleq1 exbidv ac4 vtocl ) AFZGHS
      CFISJKZAEFZLZCMTABLZCMEBDUABNUBUCCTAUABOPEACQR $.
      $( [3-May-2007] $) $( [10-Feb-1997] $)
  $}

  ${
    $d f x y A $.
    ac5.1 $e |- A e. _V $.
    $( An Axiom of Choice equivalent: there exists a function ` f ` (called a
       choice function) with domain ` A ` that maps each nonempty member of the
       domain to an element of that member.  Axiom AC of [BellMachover]
       p. 488.  Note that the assertion that ` f ` be a function is not
       necessary; see ~ ac4 . $)
    ac5 $p |- E. f ( f Fn A /\ A. x e. A ( x =/= (/) -> ( f ` x ) e. x ) ) $=
      ( vy cv wfn c0 wne cfv wcel wi wral wa wex wceq fneq2 raleq1 anbi12d
      exbidv wss cdm wal aceq3 ac4 mpgbir aceq4 mpbi a4i vtocl ) CFZEFZGZAFZHIU
      NUKJUNKLZAULMZNZCOZUKBGZUOABMZNZCOEBDULBPZUQVACVBUMUSUPUTULBUKQUOAULBRSTU
      REUKULUAUKULUBGNCOEUCZUREUCVCUPCOEEACUDEACUEUFEACUGUHUIUJ $.
      $( [25-Apr-2007] $) $( [29-Aug-1999] $)
  $}

  ${
    $d x f A $.
    ac5b.1 $e |- A e. _V $.
    $( Equivalent of Axiom of Choice. $)
    ac5b $p |- ( A. x e. A x =/= (/) ->
               E. f ( f : A --> U. A /\ A. x e. A ( f ` x ) e. x ) ) $=
      ( cv c0 wne wral wfn cfv wcel wi wa wex cuni wf ac5 19.42v crn wss
      chfnrn ex anc2li df-f syl6ibr impac r19.26 pm3.35 r19.20si sylbir sylan2
      an1s 19.22i mpan2 ) AEZFGZABHZCEZBIZUPUOURJUOKZLZABHZMZCNZBBOZURPZUTABHZM
      ZCNZABCDQUQVDMUQVCMZCNVIUQVCCRVJVHCUSUQVBVHUSVGVHUQVBMZUSVGVFUSVGUSURSVET
      ZMVFUSVGVLUSVGVLABURUAUBUCBVEURUDUEUFVKUPVAMZABHVGUPVAABUGVMUTABUPUTUHUIU
      JUKULUMUJUN $.
      $( [30-Apr-2007] $) $( [31-Aug-1999] $)
  $}

  ${
    $d f g x y z w A $.  $d f g x y z w B $.  $d ph z w f $.  $d y ps $.
    ac6.1 $e |- A e. _V $.
    ac6.2 $e |- B e. _V $.
    ${
      $d z f g C $.  $d z f g H $.
      ac6lem.4 $e |- C = { y e. B | ph } $.
      ac6lem.5 $e |- H = { <. x , z >. | ( x e. A /\ z = C ) } $.
      $( Lemma for ~ ac6 . $)
      ac6lem $p |- ( A. x e. A E. y e. B ph ->
                   E. f ( f : A --> B /\ A. x e. A ( f ` x ) e. C ) ) $=
        ( vg wrex wral cv c0 wne crn cuni wf cfv wcel wa wex wceq wi crab
        eqeq2i biimpi neeq1d rabn0 syl6bb biimprcd r19.20si r19.23v sylib cab
        abid copab rneqi rnopab eqtri eleq2i df-rex 3bitr4i syl5ib r19.21aiv
        wfn cvv rabex eqeltri fnopab2 fnex mp2an rnex ac5b ccom dffn3 mpbi fco
        mpan2 adantr wfun ax-17 hbopab1 hbxfr hbrn hbral hban 19.8a abeq2i
        sylibr expcom eleq1 sylibd vtocle fveq2 id eleq12d rcla4v syl impcom
        adantll wb cdm fnfun ax-mp fvco mp3an2 dmopab2 sylan2br fvopab2 fveq1i
        syl5eq fveq2d adantl eqtrd eleq1d adantlr mpbird ex r19.21ai ffun
        sylan jca wss unissb ssrab2 eqsstri sseq1 mpbiri a1i r19.23aiv sylbi
        mprgbir fss visset coex feq1 hbco hbeleq fveq1 ralbid anbi12d cla4ev
        19.23aiv 3syl ) ACFOZBEPZDQZRSZDITZPUUNUUNUAZNQZUBZUULUUPUCZUULUDZDUUNP
        ZUEZNUFEFHQZUBZBQZUVBUCZGUDZBEPZUEZHUFZUUKUUMDUUNUUKUULGUGZBEOZUUMUULUU
        NUDZUUKUVJUUMUHZBEPUVKUUMUHUUJUVMBEUVJUUMUUJUVJUUMACFUIZRSUUJUVJUULUVNR
        UVJUULUVNUGGUVNUULLUJUKULACFUMUNUOUPUVJUUMBEUQURUULUVDEUDZUVJUEZBUFZDUS
        ZUDUVQUVLUVKUVQDUTUUNUVRUULUUNUVPBDVAZTUVRIUVSMVBUVPBDVCVDZVEUVJBEVFVGZ
        VHVIDUUNNIIEVJZEVKUDIVKUDBDEGIGUVNVKLACFKVLVMZMVNZJEVKIVOVPZVQVRUVAUVIN
        UVAEFUUPIVSZUBZUVDUWFUCZGUDZBEPZUEZUVIUUNFUUPUBZUUTUWKUUQUWLUUTUEUWGUWJ
        UWLUWGUUTUWLEUUNIUBZUWGUWBUWMUWDEIVTWAEUUNFUUPIWBWCWDUUPWEZUUTUWJUWLUWN
        UUTUEZUWIBEUWNUUTBUWNBWFUUSBDUUNBDIBDIUVSMUVPBDDWGWHWIUUSBWFWJWKUWOUVOU
        WIUWOUVOUEUWIGUUPUCZGUDZUUTUVOUWQUWNUVOUUTUWQUVOGUUNUDZUUTUWQUHUVOUWRUH
        DGUWCUVJUVOUVLUWRUVOUVJUVLUVPUVQUVLUVPBWLUVQDUUNUVTWMWNWOUULGUUNWPWQWRU
        USUWQDGUUNUVJUURUWPUULGUULGUUPWSUVJWTXAXBXCXDXEUWNUVOUWIUWQXFUUTUWNUVOU
        EZUWHUWPGUWSUWHUVDIUCZUUPUCZUWPUWNUVDIXGZUDZUWHUXAUGZUVOUWNIWEZUXCUXDUW
        BUXEUWDEIXHXIUVDUUPIXJXKUXBEUVDBDEGIUWCMXLVEXMUVOUXAUWPUGUWNUVOUWTGUUPU
        VOUVDUVSUCZGUWTUVOGVKUDUXFGUGUWCBDEGVKXNWCUVDIUVSMXOXPXQXRXSXTYAYBYCYDU
        UNFUUPYEYFYGUUQUUOFYHZUWLUXGUULFYHZDUUNDUUNFYIUVLUVKUXHUWAUVJUXHBEUVJUX
        HUHUVOUVJUXHGFYHGUVNFLACFYJYKUULGFYLYMYNYOYPYQUUNUUOFUUPYRWCYFUVHUWKHUW
        FUUPINYSUWEYTUVBUWFUGZUVCUWGUVGUWJEFUVBUWFUUAUXIUVFUWIBEBHUWFBHUUPIUVBU
        UPUDBWFBHIUVSMUVPBDHWGWHUUBUUCUXIUVEUWHGUVDUVBUWFUUDXTUUEUUFUUGXCUUHUUI
        $.
        $( [18-Oct-1999] $)
    $}

    ${
      ac6.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
      $( Equivalent of Axiom of Choice.  This is useful for proving that there
         exists, for example, a sequence mapping natural numbers to members of
         a larger set ` B ` , where ` ph ` depends on ` x ` (the natural
         number) and ` y ` (to specify a member of ` B ` ).  A stronger version
         of this theorem, ~ ac6s , allows ` B ` to be a proper class. $)
      ac6 $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
        ( vz vw wrex wral cv wf cfv crab wcel wa wex wceq copab eqid eqeq1
        anbi2d cbvopab2v ac6lem elrab pm3.27bi r19.20si anim2i 19.22i syl ) ADF
        MCENEFGOZPZCOZUOQZADFRZSZCENZTZGUAUPBCENZTZGUAACDKEFUSGUQESZLOZUSUBZTZC
        LUCHIUSUDVHVEKOZUSUBZTCLKVFVIUBVGVJVEVFVIUSUEUFUGUHVBVDGVAVCUPUTBCEUTUR
        FSBABDURFJUIUJUKULUMUN $.
        $( [18-Oct-1999] $)
    $}
  $}

  ${
    $d x y f z A $.  $d x y f z B $.  $d f z ph $.  $d y z ps $.
    ac6s.1 $e |- A e. _V $.
    ac6s.2 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Equivalent of Axiom of Choice.  Using the Boundedness Axiom ~ bnd2 , we
       derive this strong version of ~ ac6 that doesn't require ` B ` to be a
       set. $)
    ac6s $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vz wrex wral cv wss wa wex wf bnd2 visset ac6 anim2i 19.22i fss expcom
      anim1d 19.22dv imp 19.23aiv syl ) ADFKCELJMZFNZADUJKCELZOZJPZEFGMZQZBCELZ
      OZGPZACDJEFHRUNUKEUJUOQZUQOZGPZOZJPUSUMVCJULVBUKABCDEUJGHJSITUAUBVCUSJUKV
      BUSUKVAURGUKUTUPUQUTUKUPEUJFUOUCUDUEUFUGUHUIUI $.
      $( [17-Aug-2004] $) $( [4-Feb-2004] $)

    $( Equivalent of Axiom of Choice.  Contrapositive of ~ ac6s . $)
    ac6n $p |- ( A. f ( f : A --> B -> E. x e. A ps ) ->
                E. x e. A A. y e. B ph ) $=
      ( cv wf wn wral wa wex wrex wi wal cfv wceq notbid ac6s con3i dfrex2
      imbi2i albii alinexa bitri dfral2 rexbii rexnal 3imtr4i ) EFGJZKZBLZCEMZN
      GOZLZALZDFPZCEMZLZUNBCEPZQZGRZADFMZCEPZVAUQUSUOCDEFGHDJCJUMSTABIUAUBUCVEU
      NUPLZQZGRURVDVIGVCVHUNBCEUDUEUFUNUPGUGUHVGUTLZCEPVBVFVJCEADFUIUJUTCEUKUHU
      L $.
      $( [10-Jun-2007] $) $( [10-Jun-2007] $)

    $( Generalization of the Axiom of Choice to classes.  Slightly strengthened
       version of ~ ac6s3 . $)
    ac6s2 $p |- ( A. x e. A E. y ph -> E. f ( f Fn A /\ A. x e. A ps ) ) $=
      ( wex wral cvv wrex cv wfn wa rexv ralbii wf ac6s ffn anim1i 19.22i syl
      sylbir ) ADIZCEJADKLZCEJZFMZENZBCEJZOZFIZUFUECEADPQUGEKUHRZUJOZFIULABCDEK
      FGHSUNUKFUMUIUJEKUHTUAUBUCUD $.
      $( [30-Sep-2006] $) $( [29-Sep-2006] $)

    $( Generalization of the Axiom of Choice to classes.  Theorem 10.46 of
       [TakeutiZaring] p. 97. $)
    ac6s3 $p |- ( A. x e. A E. y ph -> E. f A. x e. A ps ) $=
      ( wex wral cv wfn wa ac6s2 pm3.27 19.22i syl ) ADICEJFKELZBCEJZMZFISFIABC
      DEFGHNTSFRSOPQ $.
      $( [17-Nov-2004] $) $( [3-Nov-2004] $)
  $}

  ${
    $d f x z A $.  $d x y f z B $.  $d f z ph $.  $d z ps $.
    ac6sf.1 $e |- ( ps -> A. y ps ) $.
    ac6sf.2 $e |- A e. _V $.
    ac6sf.3 $e |- ( y = ( f ` x ) -> ( ph <-> ps ) ) $.
    $( Version of ~ ac6 with bound-variable hypothesis. $)
    ac6sf $p |- ( A. x e. A E. y e. B ph ->
                E. f ( f : A --> B /\ A. x e. A ps ) ) $=
      ( vz wrex wral cv wsbc wf wa wex cbvrexsv ralbii cfv sbhypf ac6s sylbi )
      ADFLZCEMADKNOZKFLZCEMEFGNZPBCEMQGRUEUGCEADKFSTUFBCKEFGIABDKCNUHUAHJUBUCUD
      $.
      $( [5-Mar-2008] $) $( [2-Mar-2008] $)
  $}

  ${
    $d f x y A $.  $d f y B $.
    ac6s4.1 $e |- A e. _V $.
    $( Generalization of the Axiom of Choice to proper classes. ` B ` is a
       collection ` B ( x ) ` of nonempty, possible proper classes. $)
    ac6s4 $p |- ( A. x e. A B =/= (/) ->
               E. f ( f Fn A /\ A. x e. A ( f ` x ) e. B ) ) $=
      ( vy c0 wne wral cv wcel wex wfn cfv wa n0 ralbii eleq1 ac6s2 sylbi ) CGH
      ZABIFJZCKZFLZABIDJZBMAJUENZCKZABIODLUAUDABFCPQUCUGAFBDEUBUFCRST $.
      $( [1-Oct-2006] $) $( [29-Sep-2006] $)

    $( Generalization of the Axiom of Choice to proper classes. ` B ` is a
       collection ` B ( x ) ` of nonempty, possible proper classes.  Remark
       after Theorem 10.46 of [TakeutiZaring] p. 98. $)
    ac6s5 $p |- ( A. x e. A B =/= (/) -> E. f A. x e. A ( f ` x ) e. B ) $=
      ( c0 wne wral cv wfn cfv wcel wa wex ac6s4 pm3.27 19.22i syl ) CFGABHDIZB
      JZAISKCLABHZMZDNUADNABCDEOUBUADTUAPQR $.
      $( [19-Nov-2006] $) $( [27-Mar-2006] $)
  $}

  ${
    $d x z y w v f $.
    $( An Axiom of Choice equivalent.  Given a family ` x ` of mutually
       disjoint nonempty sets, there exists a set ` y ` containing exactly one
       member from each set in the family.  Theorem 6M(4) of [Enderton]
       p. 151. $)
    ac8 $p |- ( ( A. z e. x z =/= (/) /\
                   A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
               E. y A. z e. x E! v v e. ( z i^i y ) ) $=
      ( vf cv c0 wne wral cin wceq wi wa wcel weu wex wss cdm wfn wal aceq5
      ac7 mpgbi a4i ) CGZHICAGZJUFDGZIUFUHKHLMDUGJCUGJNEGUFBGKOEPCUGJBQMZAFGZUG
      RUJUGSTNFQUIAUAAABCDEFUBAFUCUDUE $.
      $( [22-Apr-2007] $) $( [14-May-2004] $)
  $}

  ${
    $d f x A $.  $d f B $.
    ac9.1 $e |- A e. _V $.
    $( An Axiom of Choice equivalent: the infinite Cartesian product of
       nonempty classes is nonempty.  Axiom of Choice (second form) of
       [Enderton] p. 55 and its converse.  This is a stronger version of the
       axiom in Enderton, with no existence requirement for the family of
       classes ` B ( x ) ` (achieved via the Collection Principle ~ cp ). $)
    ac9s $p |- ( A. x e. A B =/= (/) <-> X_ x e. A B =/= (/) ) $=
      ( vf c0 wne wral cixp cv wfn cfv wcel wa wex ac6s4 n0 visset elixp exbii
      bitr2i sylib wceq wn wrex ixp0 con3i df-ne ralbii ralnex bitri 3imtr4i
      impbii ) CFGZABHZABCIZFGZUOEJZBKAJURLCMABHNZEOZUQABCEDPUQURUPMZEOUTEUPQVA
      USEABCURERSTUAUBUPFUCZUDCFUCZABUEZUDZUQUOVDVBABCUFUGUPFUHUOVCUDZABHVEUNVF
      ABCFUHUIVCABUJUKULUM $.
      $( [1-Oct-2006] $) $( [29-Sep-2006] $)
  $}

  ${
    $d v A $.  $d u v x y ph $.  $d ps x v $.  $d u v w x y z $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, 1 => 2. $)
    kmlem1 $p |- ( A. x ( ( A. z e. x z =/= (/) /\ A. z e. x A. w e. x ph ) ->
                 E. y A. z e. x ps ) -> A. x ( A. z e. x A. w e. x ph ->
                   E. y A. z e. x ( z =/= (/) -> ps ) ) ) $=
      ( vv vu cv c0 wne wral wa wex wi wal crab visset rabex wceq raleq1
      raleqd anbi12d exbidv imbi12d cla4v 19.21aiv wcel ssrab2 sseli imim1i
      r19.20i2 imim12i neeq1 elrab pm3.27bi rgen jctil biimpri exp3a 19.22i
      19.20i syl cbvalv sylib ) EIZJKZECIZLZAFVHLZEVHLZMZBEVHLZDNZOZCPZAFGIZLZE
      VQLZVGBOZEVQLZDNZOZGPZVKVTEVHLZDNZOZCPVPVGEHIZJKZHVQQZLZAFWJLZEWJLZMZBEWJ
      LZDNZOZGPWDVPWQGVOWQCWJWIHVQGRSVHWJTZVLWNVNWPWRVIWKVKWMVGEVHWJUAVJWLEVHWJ
      AFVHWJUAUBUCWRVMWODBEVHWJUAUDUEUFUGWQWCGVSWNWPWBVSWMWKVRWLEVQWJVFWJUHZVFV
      QUHZVRWLWJVQVFWIHVQUIZUJAAFVQWJFIZWJUHXBVQUHAWJVQXBXAUJUKULUMULVGEWJWSWTV
      GWIVGHVFVQWHVFJUNUOZUPUQURWOWADBVTEWJVQWSBOWTVGBWTVGMZWSBWSXDXCUSUKUTULVA
      UMVBVCWCWGGCVQVHTZVSVKWBWFVRVJEVQVHAFVQVHUAUBXEWAWEDVTEVQVHUAUDUEVDVE $.
      $( [12-May-2007] $) $( [5-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem2 $p |- ( E. y A. z e. x ( ph -> E! w w e. ( z i^i y ) ) <->
        E. y ( -. y e. x /\ A. z e. x ( ph -> E! w w e. ( z i^i y ) ) ) ) $=
      ( vv vu cv cin wcel weu wi wral wex wn wa wceq ineq2 eleq2d eubidv
      imbi2d ralbidv cbvexv cuni visset uniex eleq2 notbid exbidv wal nalset
      alexn mpbir a4i vtocl csn cun c0 elssuni sseld con3d disjsn syl6ibr
      impcom uneq2d un0 syl6eq indi syl5req ralbidva wo snid olci elun mpi
      con3i biantrurd bitrd snex unex eleq1 anbi12d cla4ev syl6bi 19.23aiv
      ax-mp sylbi pm3.27 19.22i impbii ) AEHZDHZCHZIZJZEKZLZDBHZMZCNZWMWRJZOZWS
      PZCNZWTAWKWLFHZIZJZEKZLZDWRMZFNXDWSXJCFWMXEQZWQXIDWRXKWPXHAXKWOXGEXKWNXFW
      KWMXEWLRSTUAUBUCXJXDFGHZWRUDZJZOZGNZXJXDLZXLWMJZOZGNZXPCXMWRBUEUFWMXMQZXS
      XOGYAXRXNWMXMXLUGUHUIXTCXTCUJXRGUJCNOCGUKXRCGULUMUNUOXOXQGXOXJXEXLUPZUQZW
      RJZOZAWKWLYCIZJZEKZLZDWRMZPZXDXOXJYJYKXOXIYIDWRXOWLWRJZPZXHYHAYMXGYGEYMXF
      YFWKYMXFWLYBIZUQZXFYFYMYOXFURUQXFYMYNURXFYLXOYNURQZYLXOXLWLJZOYPYLYQXNYLW
      LXMXLWLWRUSUTVAWLXLVBVCVDVEXFVFVGWLXEYBVHVISTUAVJXOYEYJYDXNYDXLYCJZXNYRXL
      XEJZXLYBJZVKYTYSXLGUEVLVMXLXEYBVNUMYDYCXMXLYCWRUSUTVOVPVQVRXCYKCYCXEYBFUE
      XLVSVTWMYCQZXBYEWSYJUUAXAYDWMYCWRWAUHUUAWQYIDWRUUAWPYHAUUAWOYGEUUAWNYFWKW
      MYCWLRSTUAUBWBWCWDWEWFWEWGXCWSCXBWSWHWIWJ $.
      $( [29-Mar-2004] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4.  The
       right-hand side is part of the hypothesis of 4. $)
    kmlem3 $p |- ( ( z \ U. ( x \ { z } ) ) =/= (/) <->
        E. v e. z A. w e. x ( z =/= w -> -. v e. ( z i^i w ) ) ) $=
      ( cv csn cdif cuni c0 wne cin wcel wn wi wral crab wrex wo dfdif2 cun
      dfnul3 uneq2i un0 unrab 3eqtr3i wb wa ianor wex eluni anbi1i df-rex
      eldifsn necom anbi2i bitri ancom anass 3bitri an23 elin df-an bitr3i
      3bitr3ri exbii 19.41v rexnal 3bitr2ri con1bii a1i rabbii 3eqtri neeq1i
      rabn0 ) BEZAEZVOFGZHZGZIJVOCEZJZDEZVOVTKLZMNZCVPOZDVOPZIJWEDVOQVSWFIVSWBV
      RLZMZDVOPZWHWBVOLZMZRZDVOPZWFDVOVRSWIITWIWKDVOPZTWIWMIWNWIDVOUAUBWIUCWHWK
      DVOUDUEWLWEDVOWLWEUFWJWLWGWJUGZMWEWGWJUHWEWOWOWBVTLZVTVQLZUGZCUIZWJUGZWDM
      ZCVPQZWEMWGWSWJCWBVQUJUKXBVTVPLZXAUGZCUIWRWJUGZCUIWTXACVPULXDXECWPWJUGZWQ
      UGZXCWAWJWPUGZUGZUGZXEXDXGXFXCWAUGZUGZXKXHUGZXJWQXKXFWQXCVTVOJZUGXKVTVPVO
      UMXNWAXCVTVOUNUOUPUOXLXHXKUGXMXFXHXKWPWJUQUKXHXKUQUPXCWAXHURUSWPWJWQUTXIX
      AXCXIWAWCUGXAWCXHWAWBVOVTVAUOWAWCVBVCUOVDVEWRWJCVFUSWDCVPVGVHVIVCVJVKVLVM
      WEDVOVNUP $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem4 $p |- ( ( w e. x /\ z =/= w ) ->
                    ( ( z \ U. ( x \ { z } ) ) i^i w ) = (/) ) $=
      ( vy vv cv wcel wne wa wn csn cdif cuni wral cin c0 wceq wi wal visset
      eleq1 neeq2 anbi12d eleq2 notbid imbi12d cla4v com12 eldif pm3.27 wex
      eluni notbii alnex con2b imnan eldifsn necom anbi2i bitri imbi1i 3bitr3i
      albii 3bitr2i sylib sylbi syl5 r19.21aiv disj sylibr ) CFZAFZGZBFZVKHZIZD
      FZVKGZJZDVNVLVNKLZMZLZNWBVKOPQVPVSDWBVPEFZVLGZVNWCHZIZVQWCGZJZRZESZVSVQWB
      GZWJVPVSWIVPVSREVKCTWCVKQZWFVPWHVSWLWDVMWEVOWCVKVLUAWCVKVNUBUCWLWGVRWCVKV
      QUDUEUFUGUHWKVQVNGZVQWAGZJZIZWJVQVNWAUIWPWOWJWMWOUJWOWGWCVTGZIZEUKZJWRJZE
      SWJWNWSEVQVTULUMWREUNWTWIEWGWQJRWQWHRWTWIWGWQUOWGWQUPWQWFWHWQWDWCVNHZIWFW
      CVLVNUQXAWEWDWCVNURUSUTVAVBVCVDVEVFVGVHDWBVKVIVJ $.
      $( [12-Oct-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem5 $p |- ( ( w e. x /\ z =/= w ) ->
          ( ( z \ U. ( x \ { z } ) ) i^i ( w \ U. ( x \ { w } ) ) ) = (/) ) $=
      ( cv wcel wne wa csn cdif cuni cin c0 wss wceq difss sslin ax-mp kmlem4
      sseq2d mpbii ss0b sylib ) CDZADZEBDZUCFGZUEUDUEHIJIZUCUDUCHIJZIZKZLMZUJLN
      UFUJUGUCKZMZUKUIUCMUMUCUHOUIUCUGPQUFULLUJABCRSTUJUAUB $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 4 => 1. $)
    kmlem6 $p |- ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( ph -> A = (/) ) ) ->
           A. z e. x E. v e. z A. w e. x ( ph -> -. v e. A ) ) $=
      ( cv c0 wne wral wceq wi wa wcel wn wrex r19.26 wex wal 19.29r df-rex
      sylibr n0 biimpi ne0i necon2bi imim2i r19.20si 19.21aiv syl2an sylbir )
      CGZHIZCBGZJAFHKZLZDUNJZCUNJMUMUQMZCUNJAEGZFNZOZLZDUNJZEULPZCUNJUMUQCUNQUR
      VDCUNUSULNZERZVCESZVDUMUQVFVGMVEVCMERVDVEVCETVCEULUAUBUMVFEULUCUDUQVCEUPV
      BDUNUOVAAUTFHFUSUEUFUGUHUIUJUHUK $.
      $( [12-May-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 4 => 1. $)
    kmlem7 $p |- ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
       -. E. z e. x A. v e. z E. w e. x ( z =/= w /\ v e. ( z i^i w ) ) ) $=
      ( cv c0 wne wral cin wceq wi wa wcel wn wrex kmlem6 ralinexa rexbii
      rexnal bitri ralbii ralnex sylib ) BEZFGBAEZHUDCEZGZUDUFIZFJKCUEHBUEHLUGD
      EUHMZNKCUEHZDUDOZBUEHZUGUILCUEOZDUDHZBUEONZUGABCDUHPULUNNZBUEHUOUKUPBUEUK
      UMNZDUDOUPUJUQDUDUGUICUEQRUMDUDSTUAUNBUEUBTUC $.
      $( [12-Oct-2007] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 1 <=> 4. $)
    kmlem8 $p |- ( ( -. E. z e. u A. w e. z ps -> E. y A. z e. u ( z =/= (/)
                  -> E! w w e. ( z i^i y ) ) ) <-> ( E. z e. u A. w e. z ps \/
                 E. y ( -. y e. u /\ A. z e. u E! w w e. ( z i^i y ) ) ) ) $=
      ( cv wral wrex wn c0 wne cin wcel weu wi wex wa wo wb ralnex df-rex
      rexnal bitr3i exsimpl n0 sylibr sylbir r19.20si biimt r19.15 syl anbi2d
      exbidv kmlem2 syl6rbbr pm5.74i pm4.64 bitri ) ADCFZGZCEFZHZIZUSJKZDFZUSBF
      ZLMDNZOZCVAGZBPZOVCVFVAMIZVGCVAGZQZBPZOVBVNRVCVJVNVCVDCVAGZVJVNSVCUTIZCVA
      GVOUTCVATVPVDCVAVPVEUSMZAIZQDPZVDVSVRDUSHVPVRDUSUAADUSUBUCVSVQDPVDVQVRDUD
      DUSUEUFUGUHUGVOVNVKVIQZBPVJVOVMVTBVOVLVIVKVOVGVHSZCVAGVLVISVDWACVAVDVGUIU
      HVGVHCVAUJUKULUMVDEBCDUNUOUKUPVBVNUQUR $.
      $( [12-May-2007] $) $( [4-Apr-2004] $)
  $}

  ${
    $d x y z w v u t h g $.  $d y z w v h g A $.  $d h ph $.
    kmlem9.1 $e |- A = { u | E. t e. x u = ( t \ U. ( x \ { t } ) ) } $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem9 $p |- A. z e. A A. w e. A ( z =/= w -> ( z i^i w ) = (/) ) $=
      ( vh cv wne cin c0 wceq wi csn cdif cuni wrex wcel wa reeanv ineq12
      eqeq1d kmlem5 syl5bir exp3a eqeq12 difeq1 sneq difeq2d unieqd eqtrd
      necon3d syl5d com12 adantl r19.23aivv sylbir visset eqeq1 rexbidv elab2
      eqeq2d cbvrexv bitri syl2anb rgen2a ) BIZCIZJZVHVIKZLMZNZBCFVHEIZAIZVNOZP
      ZQZPZMZEVORZVIHIZVOWBOZPZQZPZMZHVORZVMVHFSVIFSZWAWHTVTWGTZHVOREVORVMVTWGE
      HVOVOUAWJVMEHVOVOWBVOSZWJVMNVNVOSWJWKVMWJWKVNWBJZVLVJWJWKWLVLWJVLVSWFKZLM
      WKWLTWJVKWMLVHVSVIWFUBUCAEHUDUEUFWJVNWBVHVIWJVHVIMVSWFMVNWBMZVHVSVIWFUGWN
      VSWBVRPWFVNWBVRUHWNVRWEWBWNVQWDWNVPWCVOVNWBUIUJUKUJULZUEUMUNUOUPUQURDIZVS
      MZEVORZWADVHFBUSWPVHMWQVTEVOWPVHVSUTVAGVBWIVIVSMZEVORZWHWRWTDVIFCUSWPVIMW
      QWSEVOWPVIVSUTVAGVBWSWGEHVOWNVSWFVIWOVCVDVEVFVG $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem10 $p |- ( A. h ( A. z e. h A. w e. h
                              ( z =/= w -> ( z i^i w ) = (/) ) ->
                         E. y A. z e. h ph ) -> E. y A. z e. A ph ) $=
      ( cv wne cin c0 wceq wi wral wex wal kmlem9 csn cdif cuni wrex cab cvv
      visset abrexex eqeltri raleq1 raleqd exbidv imbi12d cla4v mpi ) DKZEKZLUP
      UQMNOPZEIKZQZDUSQZADUSQZCRZPZISUREHQZDHQZADHQZCRZBDEFGHJTVDVFVHPIHHFKGKZB
      KZVIUAUBUCUBZOGVJUDFUEUFJGFVJVKBUGUHUIUSHOZVAVFVCVHUTVEDUSHUREUSHUJUKVLVB
      VGCADUSHUJULUMUNUO $.
      $( [12-Oct-2007] $) $( [25-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem11 $p |- ( z e. x -> ( z i^i U. A ) = ( z \ U. ( x \ { z } ) ) ) $=
      ( cv wcel csn cdif cuni cin ciun c0 cun wceq wss snssi ssequn1 sylib
      undif2 syl5req iuneq1 syl wral wne wa kmlem4 incom syl5eq ex eldifsn
      pm3.27bi syl5 r19.21aiv iuneq2 iun0 syl6eq uneq2d iunxun visset difeq1
      sneq difeq2d unieqd eqtrd ineq2d iunxsn uneq1i eqtri un0 indif wrex cab
      unieqi cvv difexg ax-mp dfiun2 eqtr4i ineq2i iunin2 ) BGZAGZHZDWDWCDGZWDW
      FIZJZKZJZLZMZWCWDWCIZJZKZJZWCEKZLZWEWLWCWPLZNOZWPWEWLDWMWNOZWKMZWTWEWDXAP
      WLXBPWEWMWDOZWDXAWEWMWDQXCWDPWCWDRWMWDSTWMWDUAUBDWDXAWKUCUDWEWSDWNWKMZOZW
      TXBWEXDNWSWEXDDWNNMZNWEWKNPZDWNUEXDXFPWEXGDWNWEWFWCUFZXGWFWNHZWEXHXGWEXHU
      GWJWCLNWKADBUHWCWJUIUJUKXIWFWDHXHWFWDWCULUMUNUODWNWKNUPUDDWNUQURUSXBDWMWK
      MZXDOXEDWMWNWKUTXJWSXDDWCWKWSBVAWFWCPZWJWPWCXKWJWCWIJWPWFWCWIVBXKWIWOWCXK
      WHWNXKWGWMWDWFWCVCVDVEVDVFVGVHVIVJUJVFWTWSWPWSVKWCWOVLVJURWRWCDWDWJMZLWLW
      QXLWCWQCGWJPDWDVMCVNZKXLEXMFVODCWDWJWFVPHWJVPHDVAWFWIVPVQVRVSVTWADWDWCWJW
      BVTUJ $.
      $( [30-Mar-2004] $) $( [26-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 3 => 4. $)
    kmlem12 $p |- ( A. z e. x ( z \ U. ( x \ { z } ) ) =/= (/) ->
                    ( A. z e. A ( z =/= (/) -> E! v v e. ( z i^i y ) ) ->
        A. z e. x ( z =/= (/) -> E! v v e. ( z i^i ( y i^i U. A ) ) ) ) ) $=
      ( cv csn cdif cuni c0 wne wral cin wcel weu wi wceq difeq1 sneq difeq2d
      unieqd eqtrd neeq1d cbvralv ineq1d eleq2d eubidv imbi12i kmlem11 in12
      incom eqtri syl5req ax-1 syl6bi r19.20i imim2i sylbi com12 wrex cab wal
      wb raleq1 ax-mp df-ral visset eqeq1 rexbidv elab imbi1i r19.23v bitr4i
      albii ralcom4 cvv difexg neeq1 ineq1 imbi12d ceqsalv ralbii 3bitr2i
      3bitri r19.20 syl5 ) CIZAIZWJJZKZLZKZMNZCWKOZFIZWKWRJZKZLZKZMNZFWKOZDIZXB
      BIZPZQZDRZFWKOZSZWJMNZXEWJXFGLZPPZQZDRZSZCWKOZXLXEWJXFPZQZDRZSZCGOZXKWQXR
      XKWQXEWOXFPZQZDRZCWKOZSWQXRSXDWQXJYGXCWPFCWKWRWJTZXBWOMYHXBWJXAKWOWRWJXAU
      AYHXAWNWJYHWTWMYHWSWLWKWRWJUBUCUDUCUEZUFUGXIYFFCWKYHXHYEDYHXGYDXEYHXBWOXF
      YIUHUIUJUGUKYGXRWQYFXQCWKWJWKQZYFXPXQYJYEXODYJYDXNXEYJWJXMPZXFPZYDXNYJYKW
      OXFACEFGHULUHXNXFYKPYLWJXFXMUMXFYKUNUOUPUIUJXPXLUQURUSUTVAVBYCXCXISZFWKOZ
      XKYCYBCEIZXBTZFWKVCZEVDZOZWJYRQZYBSZCVEZYNGYRTYCYSVFHYBCGYRVGVHYBCYRVIUUB
      WJXBTZYBSZFWKOZCVEUUDCVEZFWKOYNUUAUUECUUAUUCFWKVCZYBSUUEYTUUGYBYQUUGEWJCV
      JYOWJTYPUUCFWKYOWJXBVKVLVMVNUUCYBFWKVOVPVQUUDFCWKVRUUFYMFWKYBYMCXBWRVSQXB
      VSQFVJWRXAVSVTVHUUCXLXCYAXIWJXBMWAUUCXTXHDUUCXSXGXEWJXBXFWBUIUJWCWDWEWFWG
      XCXIFWKWHVAWI $.
      $( [12-May-2007] $) $( [27-Mar-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 1 <=> 4. $)
    kmlem13 $p |- ( A. x ( ( A. z e. x z =/= (/) /\
           A. z e. x A. w e. x ( z =/= w -> ( z i^i w ) = (/) ) ) ->
         E. y A. z e. x E! v v e. ( z i^i y ) ) <->
          A. x ( -. E. z e. x A. v e. z E. w e. x
                     ( z =/= w /\ v e. ( z i^i w ) ) ->
               E. y A. z e. x ( z =/= (/) ->
                    E! v v e. ( z i^i y ) ) ) ) $=
      ( vh vg cv c0 wne wral cin wceq wi wa wcel weu wex wal wrex wn kmlem1
      raleq1 raleqd exbidv imbi12d cbvalv kmlem10 ineq2 eleq2d eubidv imbi2d
      ralbidv cbvexv csn cdif cuni kmlem12 visset inex1 cla4ev syl6 19.23adv
      com12 kmlem3 ralinexa rexbii rexnal 3bitri ralbii ralnex bitri syl5ibr
      sylbi syl 19.21aiv kmlem7 imim1i wb biimt r19.20si r19.15 adantr pm5.74i
      sylibr 19.20i impbii ) CLZMNZCALZOZWLDLZNZWLWPPZMQRZDWNOZCWNOZSZELZWLBLZP
      ZTZEUAZCWNOZBUBZRZAUCZWQXCWRTZSDWNUDZEWLOZCWNUDUEZWMXGRZCWNOZBUBZRZAUCZXK
      XAXRRZAUCZXTWSXGABCDUFYBWSDJLZOZCYCOZXPCYCOZBUBZRZJUCZXTYAYHAJWNYCQZXAYEX
      RYGWTYDCWNYCWSDWNYCUGUHYJXQYFBXPCWNYCUGUIUJUKYIXSAYIXPCHOZBUBZXSXPABCDFGH
      JIULYLWMXCWLKLZPZTZEUAZRZCHOZKUBZXSYKYRBKXDYMQZXPYQCHYTXGYPWMYTXFYOEYTXEY
      NXCXDYMWLUMUNUOUPUQURYSWLWNWLUSUTVAUTMNZCWNOZXRXOUUBYSXRUUBYRXRKUUBYRWMXC
      WLYMHVAZPZPZTZEUAZRZCWNOZXRAKCEFGHIVBXQUUIBUUDYMUUCKVCVDXDUUDQZXPUUHCWNUU
      JXGUUGWMUUJXFUUFEUUJXEUUEXCXDUUDWLUMUNUOUPUQVEVFVGVHUUBXNUEZCWNOXOUUAUUKC
      WNUUAWQXLUERDWNOZEWLUDXMUEZEWLUDUUKACDEVIUULUUMEWLWQXLDWNVJVKXMEWLVLVMVNX
      NCWNVOVPVQVRVSVTVRVSXSXJAXSXBXRRXJXBXOXRACDEWAWBXBXIXRWOXIXRWCXAWOXHXQBWO
      XGXPWCZCWNOXHXQWCWMUUNCWNWMXGWDWEXGXPCWNWFVSUIWGWHWIWJWK $.
      $( [12-Oct-2007] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x y z w v u $.  $d u ph $.
    kmlem14.1 $e |- ( ph <->
                ( z e. y -> ( ( v e. x /\ y =/= v ) /\ z e. v ) ) ) $.
    kmlem14.2 $e |- ( ps <-> ( z e. x -> ( ( v e. z /\ v e. y ) /\
                ( ( u e. z /\ u e. y ) -> u = v ) ) ) ) $.
    kmlem14.3 $e |- ( ch <->
                A. z e. x E! v v e. ( z i^i y ) ) $.
    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 5 <=> 4. $)
    kmlem14 $p |- ( E. z e. x A. v e. z E. w e. x
                   ( z =/= w /\ v e. ( z i^i w ) ) <->
                 E. y A. z E. v A. u ( y e. x /\ ph ) ) $=
      ( cv wne cin wcel wa wrex wral wex wal wceq neeq1 ineq1 eleq2d anbi12d
      rexbidv raleqd cbvrexv df-rex wi eleq1 anbi2d cbvralv df-ral bitri
      anbi2i 19.28v neeq2 ineq2 imbi2i 19.37v bitr4i 19.42v ax-17 19.3 elin
      baibr anass syl6bb pm5.74i bitr2i exbii 3bitr2i albii 3bitri ) FMZGMZNZHM
      ZVQVROZPZQZGDMZRZHVQSZFWDREMZVRNZVTWGVROZPZQZGWDRZHWGSZEWDRWGWDPZWMQZETWN
      AQZIUAZHTZFUAZETWFWMFEWDWEWLHVQWGVQWGUBZWCWKGWDWTVSWHWBWJVQWGVRUCWTWAWIVT
      VQWGVRUDUEUFUGUHUIWMEWDUJWOWSEWOWNVQWGPZWHVQWIPZQZGWDRZUKZFUAZQWNXEQZFUAW
      SWMXFWNWMXDFWGSXFWLXDHFWGVTVQUBZWKXCGWDXHWJXBWHVTVQWIULUMUGUNXDFWGUOUPUQW
      NXEFURXGWRFXGWNXAVTWDPZWGVTNZVQWGVTOZPZQZQZUKZHTZQWNXOQZHTWRXEXPWNXEXAXNH
      TZUKXPXDXRXAXDXMHWDRXRXCXMGHWDVRVTUBZWHXJXBXLVRVTWGUSXSWIXKVQVRVTWGUTUEUF
      UIXMHWDUJUPVAXAXNHVBVCUQWNXOHVDXQWQHWQWPXQWPIWPIVEVFAXOWNAXAXIXJQZVQVTPZQ
      ZUKXOJXAYBXNXAYBXTXLQXNXAYAXLXTXLXAYAVQWGVTVGVHUMXIXJXLVIVJVKUPUQVLVMVNVO
      VNVMVP $.
      $( [12-Oct-2007] $) $( [4-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4, part of 5 <=> 4. $)
    kmlem15 $p |- ( ( -. y e. x /\ ch ) <->
                     A. z E. v A. u ( -. y e. x /\ ps ) ) $=
      ( cv wcel wn wa wal wex cin weu wral wceq wi wsbc ax-17 eu1 elin eleq1
      sbie bitri eqcom imbi12i albii anbi12i 19.28v bitr4i exbii ralbii df-ral
      19.21v 19.37v 3bitri anbi2i 19.42v bitr2i 3bitr2i ) ELZDLZMNZCOVHBHPZGQZF
      PZOVHVJOZFPVHBOHPZGQZFPCVKVHCGLZFLZVFRZMZGSZFVGTVOVPMVOVFMOZHLZVPMWAVFMOZ
      WAVOUAZUBZOZHPZGQZFVGTZVKKVSWGFVGVSVRVRGWAUCZVOWAUAZUBZHPZOZGQWGVRGHVRHUD
      UEWMWFGWMVTWDHPZOWFVRVTWLWNVOVPVFUFWKWDHWIWBWJWCWIWAVQMZWBVRWOGHWOGUDVOWA
      VQUGUHWAVPVFUFUIVOWAUJUKULUMVTWDHUNUOUPUIUQWHVPVGMZWGUBZFPVKWGFVGURVJWQFV
      JWPWFUBZGQWQVIWRGVIWPWEUBZHPWRBWSHJULWPWEHUSUIUPWPWFGUTUIULUOVAVBVHVJFUNV
      LVNFVNVHVIOZGQVLVMWTGVHBHUNUPVHVIGVCVDULVE $.
      $( [10-Apr-2004] $) $( [4-Apr-2004] $)

    $( Lemma for 5-quantifier AC of Kurt Maes, Th. 4 5 <=> 4. $)
    kmlem16 $p |- ( ( E. z e. x A. v e. z E. w e. x
       ( z =/= w /\ v e. ( z i^i w ) ) \/ E. y ( -. y e. x /\ ch ) ) <->
       E. y A. z E. v A. u ( ( y e. x /\ ph ) \/ ( -. y e. x /\ ps ) ) ) $=
      ( cv wne cin wcel wa wrex wral wn wex wo wal kmlem14 kmlem15 exbii
      orbi12i 19.43 wb pm3.24 pm3.26 a4s 19.23aivv anim12i mto 19.33b ax-mp
      19.23aiv bitr2i albii bitr3i 3bitr2i ) FMZGMZNHMVCVDOPQGDMZRHVCSFVERZEMVE
      PZTZCQZEUAZUBVGAQZIUCZHUAZFUCZEUAZVHBQZIUCZHUAZFUCZEUAZUBVNVSUBZEUAVKVPUB
      IUCZHUAZFUCZEUAVFVOVJVTABCDEFGHIJKLUDVIVSEABCDEFHIJKLUEUFUGVNVSEUHWAWDEWA
      VMVRUBZFUCZWDVMFUAZVRFUAZQZTWFWAUIWIVGVHQZVGUJZWGVGWHVHVLVGFHVKVGIVGAUKZU
      LUMVQVHFHVPVHIVHBUKZULUMUNUOVMVRFUPUQWEWCFWCVLVQUBZHUAWEWBWNHVKIUAZVPIUAZ
      QZTWBWNUIWQWJWKWOVGWPVHVKVGIWLURVPVHIWMURUNUOVKVPIUPUQUFVLVQHUHUSUTVAUFVB
      $.
      $( [12-Oct-2007] $) $( [4-Apr-2004] $)
  $}

  ${
    $d x y z w v u f t h $.
    $( Equivalence of the Axiom of Choice (first form) of [Enderton] p. 49 and
       Maes' AC ~ ackm .  The proof consists of lemmas ~ kmlem1 through
       ~ kmlem16 and this final theorem.  AC is not used for the proof.  Note:
       bypassing the first step (i.e. replacing ~ aceq5 with ~ biid )
       establishes the AC equivalence shown by Mae's writeup.  The
       left-hand-side AC shown here was chosen because it is shorter to
       display. $)
    aceqkm $p |- ( A. x E. f ( f C_ x /\ f Fn dom x ) <->
      A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) ) $=
      ( vw vt vh cv wss cdm wfn wa wex wal c0 wne wral cin wceq wi wcel weu
      wrex wn wo aceq5 csn cdif cuni cab eqid kmlem13 kmlem8 albii bitri df-ne
      bicomi anbi2i anbi1i imbi2i biid kmlem16 3bitri ) FJZAJZKVFVGLMNFOAPCJZQR
      ZCVGSVHGJZRZVHVJTZQUAUBGVGSCVGSNDJZVHBJZTUCDUDZCVGSZBOUBAPZVKVMVLUCNGVGUE
      ZDVHSCVGUEZVNVGUCZUFZVPNBOUGZAPZVTVHVNUCZVMVGUCZVNVMUAUFZNZVHVMUCZNZUBZNW
      AVHVGUCVMVHUCVMVNUCNEJZVHUCWKVNUCNWKVMUAUBNUBZNUGEPDOCPBOZAPABCGDFUHVQVSU
      FVIVOUBCVGSBOUBZAPWCABCGDHIHJIJZVGWOUIUJUKUJUAIVGUEHULZWPUMUNWNWBAVRBCDAU
      OUPUQWBWMAWJWLVPABCGDEWIWEVNVMRZNZWHNWDWGWRWHWFWQWEWQWFVNVMURUSUTVAVBWLVC
      VPVCVDUPVE $.
      $( [6-May-2004] $) $( [13-Apr-2004] $)
  $}

  ${
    $d x y z w v u f t h $.
    $( A remarkable equivalent to the Axiom of Choice that has only 5
       quantifiers (when expanded to ` e. ` , ` = ` primitives in prenex form),
       discovered and proved by Kurt Maes.  This establishes a new record,
       reducing from 6 to 5 the largest number of quantified variables needed
       by any ZFC axiom.  The ZF-equivalence to AC is shown by theorem
       ~ aceqkm .  Maes found this version of AC in April, 2004 (replacing a
       longer version, also with 5 quantifiers, that he found in November,
       2003).  See Kurt Maes, "A 5-quantifier ( ` e. ` ,=)-expression
       ZF-equivalent to the Axiom of Choice"
       ( ~ http://arxiv.org/PS_cache/arxiv/pdf/0705/0705.3162v1.pdf ).

       The original FOM posts are:
       ~ http://www.cs.nyu.edu/pipermail/fom/2003-November/007631.html
       ~ http://www.cs.nyu.edu/pipermail/fom/2003-November/007641.html . $)
    ackm $p |- A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) $=
      ( vf cv wss cdm wfn wa wex wcel wceq wn wi wo wal aceqkm ac7 mpgbi ) FGZA
      GZHUBUCIJKFLBGZUCMZCGZUDMDGZUCMUDUGNOKUFUGMKPKUEOUFUCMUGUFMUGUDMKEGZUFMUH
      UDMKUHUGNPKPKQERDLCRBLARAABCDEFSAFTUA $.
      $( [6-May-2004] $) $( [29-Apr-2004] $)

$(
    @( DELETE THIS @)
    ackmxx @p |- ( A. x E. y A. z E. v A. u (
       ( y e. x /\ ( z e. y -> ( ( v e. x /\ -. y = v ) /\ z e. v ) ) ) \/
           ( -. y e. x /\ ( z e. x -> ( ( v e. z /\ v e. y ) /\
               ( ( u e. z /\ u e. y ) ->
                   u = v ) ) ) ) ) <-> ph ) @=
      ( cv wcel wceq wn wa wi wo wal wex df-ex df-or df-an imbi1i notbii bitri
      imbi2i imbi12i albii bicomi ) ACGZBGZHZDGZUFHZEGZUGHZUFUKIJZKZUIUKHZKZLZK
      ZUHJZUIUGHZUKUIHZUKUFHZKZFGZUIHZVDUFHZKZVDUKIZLZKZLZKZMZFNZEOZDNZCOZBNZAU
      HUJULUMJLJZUOJZLZJZLZJZLZJZJZUSUTVAVBJLJZVEVFJLJZVHLZJZLZJZLZJZLZJZLZFNZJ
      ZENZJZDNZJZCNZJZBNZVR?VRXGVQXFBVQVPJZCNZJXFVPCPXIXEXHXDCVPXCVOXBDVOVNJZEN
      ZJXBVNEPXKXAXJWTEVNWSVMWRFVMURJZVLLWRURVLQXLWGVLWQURWFURUHUQJZLZJWFUHUQRX
      NWEXMWDUHUQWCUPWBUJUPUNVTLZJWBUNUORXOWAUNVSVTULUMRSTUAUBTUBTUATVLUSVKJZLZ
      JWQUSVKRXQWPXPWOUSVKWNVJWMUTVJVCVIJZLZJWMVCVIRXSWLVCWHXRWKVAVBRVIWJVGWIVH
      VEVFRSTUCTUAUBTUBTUAUCUAUDTUDTUAUDTUDTUAUDUEUAUE @.
      @( [ ?] @) @( [2-Feb-2006] @)

    axacxx @p  |- ( E. y A. z A. w ( ( z e. w /\ w e. x ) -> E. v A. u ( E. t
              ( ( u e. w /\ w e. t ) /\ ( u e. t /\ t e. y ) ) <-> u = v ) )
<-> ph ) @=
      ( cv wcel wa wex wceq wb wal wi wn df-ex df-an dfbi1 notbii imbi12i bitri
      albii imbi1i imbi2i bicomi ) ADIEIZJZUHBIJZKZGIZUHJZUHHIZJZKZULUNJZUNCIJZ
      KZKZHLZULFIMZNZGOZFLZPZEOZDOZCLZAUIUJQPQZUMUOQPQZUQURQPQZQZPZQZQZHOZQZVBP
      ZVBVRPZQZPZQZGOZQZFOZQZPZEOZDOZQZCOZQZVI?VIWMVIVHQZCOZQWMVHCRWOWLWNWKCVHW
      JVGWIDVFWHEUKVJVEWGUIUJSVEVDQZFOZQWGVDFRWQWFWPWEFVDWDVCWCGVCVAVBPZVBVAPZQ
      ZPZQWCVAVBTXAWBWRVSWTWAVAVRVBVAUTQZHOZQVRUTHRXCVQXBVPHUTVOUTUPUSQZPZQVOUP
      USSXEVNUPVKXDVMUMUOSUSVLUQURSUAUBUAUCUAUDUAUCZUEWSVTVAVRVBXFUFUAUBUAUCUDU
      AUDUAUCUBUDUDUAUDUAUCUGUCUG @.
      @( [ ?] @) @( [2-Feb-2006] @)

Maes expanded to primitives -  94 symbols
A.  4
e. 10
-> 11
-. 17
(  11
)  11
=   2
var occ 28
vars 5

-. A. y -. A. z -. A. v -. A. u ( ( y e. x -> -. ( z e. y -> -. ( -. ( v
e. x -> y = v ) -> -. z e. v ) ) ) -> -. ( -. y e. x -> -. ( z e. x ->
-. ( -. ( v e. z -> -. v e. y ) -> -. ( -. ( u e. z -> -. u e. y ) -> u
= v ) ) ) ) )

ax-ac expanded to primitives - 99 symbols

A.  7
e. 10
-> 11
-. 16
(  11
)  11
=   2
var occ 31
vars 7

-. A. y -. A. z A. w ( -. ( z e. w -> -. w e. x ) -> -. A. v -. A. u -.
( ( -. A. t ( -. ( u e. w -> -. w e. t ) -> ( u e. t -> -. t e. y ) ) ->
u = v ) -> -. ( u = v -> -. A. t ( -. ( u e. w -> -. w e. t ) -> ( u e.
t -> -. t e. y ) ) ) ) )
$)
  $}

