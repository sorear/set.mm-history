$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Cardinal number theorems using Axiom of Choice
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.
    $( The value of the cardinal number function.  Definition 10.4 of
       [TakeutiZaring] p. 85.  See ~ cardval2 for a simpler version of its
       value. $)
    cardval $p |- ( card ` A ) = |^| { x e. On | x ~~ A } $=
      ( vz vy cvv wcel ccrd cfv cv cen wbr con0 crab cint wceq copab df-card
      fveq1i wrex numth2 intexrab mpbi breq2 rabbidv inteqd fvopabg mpan2
      syl5eq wn c0 fvprc cab wa visset enref brprc mpbiri biantrud abbidv
      df-rab syl6reqr abid2 syl6eq inton eqtr4d pm2.61i ) BEFZBGHZAIZBJKZALMZNZ
      OVGVHBCIVIDIZJKZALMZNZODCPZHZVLBGVQDCAQRVGVLEFZVRVLOVJALSVSABTVJALUAUBDCB
      VPVLEEVMBOZVOVKVTVNVJALVMBVIJUCUDUEUFUGUHVGUIZVHUJVLBGUKWAVLLNUJWAVKLWAVK
      VILFZAULZLWAWCWBVJUMZAULVKWAWBWDAWAVJWBWAVJVIVIJKVIAUNUOVIBJUPUQURUSVJALU
      TVAALVBVCUEVDVCVEVF $.
      $( [21-Oct-2003] $)

    $( Any set is equinumerous to its cardinal number.  Proposition 10.5 of
       [TakeutiZaring] p. 85. $)
    cardid $p |- ( card ` A ) ~~ A $=
      ( vx ccrd cfv cv cen wbr con0 crab wcel cint cardval wss c0 wne ssrab2
      cvv fvex eqeltrri intex mpbir onint mp2an eqeltri breq1 elrab simprbi
      ax-mp ) ACDZBEZAFGZBHIZJZUIAFGZUIULKZULBALZULHMULNOZUOULJUKBHPUQUOQJUIUOQ
      UPACRSULTUAULUBUCUDUMUIHJUNUKUNBUIHUJUIAFUEUFUGUH $.
      $( [22-Oct-2003] $)
  $}

  $( Two sets are equinumerous iff their cardinal numbers are equal.  This
     important theorem expresses the essential concept behind "cardinality" or
     "size."  This theorem appears as Proposition 10.10 of [TakeutiZaring]
     p. 85, Theorem 7P of [Enderton] p. 197, and Theorem 9 of [Suppes] p. 242
     (among others).  The Axiom of Choice is required for its proof.

     The theory of cardinality can also be developed without AC by introducing
     "card" as a primitive notion and stating this theorem as an axiom, as is
     done with the axiom for cardinal numbers in [Suppes] p. 111.  Finally, if
     we allow the Axiom of Regularity, we can avoid AC by defining the cardinal
     number of a set as the set of all sets equinumerous to it and having least
     possible rank (see ~ karden ). $)
  carden $p |- ( ( A e. C /\ B e. D ) ->
               ( ( card ` A ) = ( card ` B ) <-> A ~~ B ) ) $=
    ( wcel wa ccrd cfv wceq cen wbr wi cardid ensymg mpi breq2 entr mpan2
    syl6bi syl5com adantr wss mpan wn cardne con2i con0 wb cardon ontri1 mp2an
    sylibr syl syl6 a1i jcad eqss syl6ibr adantl impbid ) ACEZBDEZFAGHZBGHZIZAB
    JKZVAVEVFLVBVAAVCJKZVEVFVAVCAJKZVGAMZVCACNOVEVGAVDJKZVFVCVDAJPVJVDBJKZVFBMZ
    AVDBQRSTUAVBVFVELVAVBVFVCVDUBZVDVCUBZFVEVBVFVMVNVBVFBAJKZVMABDNVOVDAJKZVMVK
    VOVPVLVDBAQUCVPVDVCEZUDZVMVQVPVDAUEUFVCUGEZVDUGEZVMVRUHAUIZBUIZVCVDUJUKULUM
    UNVFVNLVBVFVCBJKZVNVHVFWCVIVCABQUCWCVCVDEZUDZVNWDWCVCBUEUFVTVSVNWEUHWBWAVDV
    CUJUKULUMUOUPVCVDUQURUSUT $.
    $( [22-Oct-2003] $)

  $( Only the empty set has cardinality zero. $)
  cardeq0 $p |- ( A e. V -> ( ( card ` A ) = (/) <-> A = (/) ) ) $=
    ( wcel ccrd cfv c0 wceq cen wbr cvv wb 0ex carden mpan2 card0 eqeq2i en0
    3bitr3g ) ABCZADEZFDEZGZAFHIZTFGAFGSFJCUBUCKLAFBJMNUAFTOPAQR $.
    $( [23-Apr-2004] $)

  ${
    unsnen.1 $e |- A e. _V $.
    unsnen.2 $e |- B e. _V $.
    $( Equinumerosity of a set with a new element added. $)
    unsnen $p |- ( -. B e. A -> ( A u. { B } ) ~~ suc ( card ` A ) ) $=
      ( wcel wn csn cun ccrd cfv csuc cen cin c0 wceq wbr disjsn word cardon
      onordi orddisj ax-mp wa cardid ensymi cvv fvex en2sn mp2an unen mpanl12
      mpan2 sylbir df-suc syl6breqr ) BAEFZABGZHZAIJZUSGZHZUSKLUPAUQMNOZURVALPZ
      ABQVBUSUTMNOZVCUSRVDUSASTUSUAUBAUSLPUQUTLPZVBVDUCVCUSACAUDUEBUFEUSUFEVEDA
      IUGBUSUFUFUHUIAUSUQUTUJUKULUMUSUNUO $.
      $( [7-Nov-2008] $)
  $}

  $( Two sets have the dominance relationship if their cardinalities have the
     subset relationship. $)
  carddomi $p |- ( A e. V -> ( ( card ` A ) C_ ( card ` B ) -> A ~<_ B ) ) $=
    ( wcel ccrd cfv wss cdom wbr wa cen cardid ensymg mpi con0 wi cardon
    ssdomg ax-mp endomtr syl2an domentr sylancl ex ) ACDZAEFZBEFZGZABHIZUEUHJAU
    GHIZUGBKIUIUEAUFKIZUFUGHIZUJUHUEUFAKIUKALUFACMNUFODUHULPAQUFUGORSAUFUGTUABL
    AUGBUBUCUD $.
    $( [22-Oct-2003] $)

  $( Two sets have the dominance relationship iff their cardinalities have the
     subset relationship.  Equation i of [Quine] p. 232. $)
  carddom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) C_ ( card ` B ) <-> A ~<_ B ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr wi carddomi adantr csdm cen wo brdom2 wn
    cardon onelssi syl5 domnsym syl6 con2d con0 wb ontri1 mp2an syl6ibr adantl
    wceq carden eqimss syl6bir jaod syl5bi impbid ) ACEZBDEZFZAGHZBGHZIZABJKZUS
    VDVELUTABCMNVEABOKZABPKZQVAVDABRVAVFVDVGUTVFVDLUSUTVFVCVBEZSZVDUTVHVFUTVHBA
    JKZVFSVHVCVBIUTVJVBVCATZUABADMUBBAUCUDUEVBUFEVCUFEVDVIUGVKBTVBVCUHUIUJUKVAV
    GVBVCULVDABCDUMVBVCUNUOUPUQUR $.
    $( [22-Oct-2003] $)

  $( Two sets have the strict dominance relationship iff their cardinalities
     have the membership relationship.  Corollary 19.7(2) of [Eisenberg]
     p. 310. $)
  cardsdom $p |- ( ( A e. V /\ B e. W ) ->
               ( ( card ` A ) e. ( card ` B ) <-> A ~< B ) ) $=
    ( wcel wa ccrd cfv wss wne cdom wbr cen wn csdm carddom carden necon3abid
    anbi12d con0 wb cardon onelpss mp2an brsdom 3bitr4g ) ACEBDEFZAGHZBGHZIZUHU
    IJZFZABKLZABMLZNZFUHUIEZABOLUGUJUMUKUOABCDPUGUNUHUIABCDQRSUHTEUITEUPULUAAUB
    BUBUHUIUCUDABUEUF $.
    $( [22-Oct-2003] $)

  $( Trichotomy law for dominance and strict dominance.  This theorem is
     equivalent to the Axiom of Choice. $)
  domtri $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B <-> -. B ~< A ) ) $=
    ( wcel wa ccrd cfv wss cdom wbr csdm wn carddom con0 wb cardon ontri1
    mp2an cardsdom ancoms notbid syl5bb bitr3d ) ACEZBDEZFZAGHZBGHZIZABJKBALKZM
    ZABCDNUJUIUHEZMZUGULUHOEUIOEUJUNPAQBQUHUIRSUGUMUKUFUEUMUKPBADCTUAUBUCUD $.
    $( [4-Jan-2004] $)

  $( Trichotomy of equinumerosity and strict dominance.  This theorem is
     equivalent to the Axiom of Choice.  Theorem 8 of [Suppes] p. 242. $)
  entric $p |- ( ( A e. V /\ B e. W ) -> ( A ~< B \/ A ~~ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen wo w3o wn cdom domtri biimprd brdom2 syl6ib con1d
    orrd df-3or sylibr ) ACEBDEFZABGHZABIHZJZBAGHZJUCUDUFKUBUEUFUBUFUEUBUFLZABM
    HZUEUBUHUGABCDNOABPQRSUCUDUFTUA $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance and strict dominance. $)
  entri2 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~< A ) ) $=
    ( wcel wa csdm wbr cen w3o cdom wo entric brdom2 orbi1i df-3or bitr4i
    sylibr ) ACEBDEFABGHZABIHZBAGHZJZABKHZUALZABCDMUDSTLZUALUBUCUEUAABNOSTUAPQR
    $.
    $( [1-Oct-2004] $) $( [4-Jan-2004] $)

  $( Trichotomy of dominance.  This theorem is equivalent to the Axiom of
     Choice.  Part of Proposition 4.42(d) of [Mendelson] p. 275. $)
  entri3 $p |- ( ( A e. V /\ B e. W ) -> ( A ~<_ B \/ B ~<_ A ) ) $=
    ( wcel wa cdom wbr csdm wo entri2 sdomdom orim2i syl ) ACEBDEFABGHZBAIHZJOB
    AGHZJABCDKPQOBALMN $.
    $( [13-Nov-2004] $) $( [4-Jan-2004] $)

  $( A set strictly dominates iff its cardinal strictly dominates. $)
  sdomsdomcard $p |- ( A ~< B <-> A ~< ( card ` B ) ) $=
    ( csdm wbr cvv wcel ccrd cfv sdomex simprd wn c0 sdom0 fvprc breq2d mtbiri
    con4i cen cardid ensymg mpi wa wi fvex sdomentr ax-mp ex syl5com mpan2i
    impbid pm5.21nii ) ABCDZBEFZABGHZCDZULAEFUMABIJUMUOUMKZUOALCDAMUPUNLACBGNOP
    QUMULUOUMBUNRDZULUOUMUNBRDZUQBSZUNBETUAULUQUOUNEFULUQUBUOUCBGUDABUNEUEUFUGU
    HUMUOURULUSAUNBEUEUIUJUK $.
    $( [30-Oct-2003] $)

  $( Cantor's theorem in terms of cardinals.  This theorem tells us that no
     matter how largei a cardinal number is, there is a still larger cardinal
     number.  Theorem 18.12 of [Monk1] p. 133. $)
  canth3 $p |- ( A e. V -> ( card ` A ) e. ( card ` ~P A ) ) $=
    ( wcel ccrd cfv cpw csdm wbr canth2g cvv wb pwexg cardsdom mpdan mpbird )
    ABCZADEAFZDECZAQGHZABIPQJCRSKABLAQBJMNO $.
    $( [11-Jul-2004] $) $( [5-Nov-2003] $)

  ${
    $d A x $.
    infxpidm.1 $e |- A e. _V $.
    $( The cross product of an infinite set with itself is idempotent.  This
       theorem (which is an AC equivalent) provides the basis for infinite
       cardinal arithmetic.  Proposition 10.40 of [TakeutiZaring] p. 95.  This
       proof follows as a corrolary of ~ infxpen .  (Revised by Mario Carneiro,
       9-Mar-2013.) $)
    infxpidm $p |- ( om ~<_ A -> ( A X. A ) ~~ A ) $=
      ( com cdom wbr ccrd cfv cxp cen wss cvv wcel wb omex carddom mp2an
      cardom sseq1i bitr3i con0 cardon infxpen mpan sylbi cardid entr mpan2
      ensymi fvex xpen 3syl ) CADEZAFGZUMHZUMIEZUNAIEZAAHZAIEZULCUMJZUOULCFGZUM
      JZUSCKLAKLVAULMNBCAKKOPUTCUMQRSUMTLUSUOAUAUMUBUCUDUOUMAIEUPAUEZUNUMAUFUGU
      QUNIEZUPURAUMIEZVDVCUMABVBUHZVEAUMAUMAFUIZVFUJPUQUNAUFUCUK $.
      $( [9-Mar-2013] $) $( [17-Sep-2004] $)
  $}

  $( A cardinal strictly dominates its members.  Equivalent to Proposition
     10.37 of [TakeutiZaring] p. 93 (use ~ cardsdom to obtain the exact
     proposition from this one). $)
  cardsdomel $p |- ( A e. On -> ( A ~< B <-> A e. ( card ` B ) ) ) $=
    ( csdm wbr ccrd cfv con0 wcel sdomsdomcard wss cdom wn ssdom2g wb cardon
    ontri1 mpan domtri 3imtr3d con4d cen onelssi mpsyl cardidm eleq2i cardne
    sylbir brsdom sylanbrc impbid1 syl5bb ) ABCDABEFZCDZAGHZAULHZABIUNUMUOUNUOU
    MUNULAJZULAKDZUOLZUMLZULAGMULGHZUNUPURNBOZULAPQUTUNUQUSNVAULAGGRQSTUOAULKDZ
    AULUADLZUMUTUOAULJVBVAULAVAUBAULGMUCUOAULEFZHVCVDULABUDUEAULUFUGAULUHUIUJUK
    $.
    $( [2-Nov-2003] $)

  ${
    $d x A $.
    $( An alternate version of the value of the cardinal number of a set.
       Compare ~ cardval .  This theorem could be used to give us a simpler
       definition of ` card ` in place of ~ df-card .  It apparently does not
       occur in the literature. $)
    cardval2 $p |- ( card ` A ) = { x e. On | x ~< A } $=
      ( cv csdm wbr con0 crab wcel wa cab ccrd cfv df-rab cardon oneli
      pm4.71ri cardsdomel pm5.32i bitr4i abbii abid2 3eqtr2ri ) ACZBDEZAFGUCFHZ
      UDIZAJUCBKLZHZAJUGUDAFMUHUFAUHUEUHIUFUHUEUGUCBNOPUEUDUHUCBQRSTAUGUAUB $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y z A $.
    $( The collection of ordinal numbers dominated by a set is an ordinal
       number.  (In general, not all collections of ordinal numbers are
       ordinal.)  Theorem 56 of [Suppes] p. 227.  This theorem can be proved
       (with a longer proof) without the Axiom of Choice; see ~ hartogs . $)
    ondomon $p |- ( A e. V -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz wcel cv cdom wbr con0 crab word wtr wss wa wi wal onelon cvv
      visset onelss imp ssdomg mpsyl jca domtr anim2i anassrs sylan exp31
      com12 imp3a breq1 elrab 3imtr4g gen2 dftr2 mpbir ssrab2 ordon trssord
      mp3an cpw csdm wb wral elisset canth2g domsdomtr sylan2 expcom a1d
      r19.21aiv syl ss2rab sylibr ccrd cfv cardval2 fvex eqeltrri ssex elong
      3syl mpbiri ) BCFZAGZBHIZAJKZJFZWILZWIMZWIJNJLWKWLDGZEGZFZWNWIFZOWMWIFZPZ
      EQDQWRDEWOWPWQWOWNJFZWNBHIZOWMJFZWMBHIZOZWPWQWOWSWTXCWSWOWTXCPWSWOWTXCWSW
      OOZXAWMWNHIZOWTXCXDXAXEWNWMRWMSFXDWMWNNZXEDTWSWOXFWNWMUAUBWMWNSUCUDUEXAXE
      WTXCXEWTOXBXAWMWNBUFUGUHUIUJUKULWHWTAWNJWGWNBHUMUNWHXBAWMJWGWMBHUMUNUOUBU
      PDEWIUQURWHAJUSUTWIJVAVBWFWIWGBVCZVDIZAJKZNZWISFWJWKVEWFWHXHPZAJVFZXJWFBS
      FZXLBCVGXMXKAJXMXKWGJFWHXMXHXMWHBXGVDIXHBSVHWGBXGVIVJVKVLVMVNWHXHAJVOVPWI
      XIXGVQVRXISAXGVSXGVQVTWAWBWISWCWDWE $.
      $( [7-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d y V $.
    $( The smallest ordinal that strictly dominates a set is a cardinal. $)
    cardmin $p |- ( A e. V -> ( card ` |^| { x e. On | A ~< x } ) =
                      |^| { x e. On | A ~< x } ) $=
      ( vy wcel cv csdm wbr con0 crab cint wral ccrd cfv wceq wrex numthcor
      onintrab2 sylib cdom wa wn wi onelon ex syl breq2 elrab wss ssrab2
      onnmin mpan sylbir con2d syli cvv wb visset domtri sylibrd ax-17 hbrab1
      hbint hbbr onminsb jctird domsdomtr syl6 r19.21aiv iscard sylanbrc ) BCEZ
      BAFZGHZAIJZKZIEZDFZVPGHZDVPLVPMNVPOVLVNAIPZVQABCQZVNARSZVLVSDVPVLVRVPEZVR
      BTHZBVPGHZUAVSVLWCWDWEVLWCBVRGHZUBZWDWCVLVRIEZWGVLVQWCWHUCWBVQWCWHVPVRUDU
      EUFWHWFWCWHWFWCUBZWHWFUAVRVOEZWIVNWFAVRIVMVRBGUGUHVOIUIWJWIVNAIUJVOVRUKUL
      UMUEUNUOVRUPEVLWDWGUQDURVRBUPCUSULUTVLVTWEWAVNWEAADBVPGVRBEAVAVRGEAVAADVO
      VNADIVBVCVDVMVPBGUGVEUFVFVRBVPVGVHVIDVPVJVK $.
      $( [28-Oct-2003] $)
  $}

  ${
    $d A x $.  $d V x $.
    $( A set is finite iff its cardinal is a natural number.  (Contributed by
       Jeff Madsen 2-Sep-2009.) $)
    ficard $p |- ( A e. V -> ( A e. Fin <-> ( card ` A ) e. om ) ) $=
      ( vx wcel cfn ccrd cfv com cv cen wbr wrex isfi wa wceq carden wi cardnn
      eqtr expcom syl eleq1a syld adantl sylbird r19.23adva syl5bi eqcomd
      mpbid ex ancld breq2 rcla4ev sylibr syl6 impbid ) ABDZAEDZAFGZHDZURACIZJK
      ZCHLZUQUTCAMZUQVBUTCHUQVAHDZNVBUSVAFGZOZUTAVABHPVEVGUTQUQVEVGUSVAOZUTVEVF
      VAOZVGVHQVARVGVIVHUSVFVASTUAVAHUSUBUCUDUEUFUGUQUTUTAUSJKZNZURUQUTVJUQUTVJ
      UQUTNUSUSFGZOZVJUTVMUQUTVLUSUSRUHUDAUSBHPUIUJUKVKVCURVBVJCUSHVAUSAJULUMVD
      UNUOUP $.
      $( [2-Sep-2009] $)
  $}

  ${
    konigth.1 $e |- A e. _V $.
    konigth.2 $e |- S = U_ i e. A ( M ` i ) $.
    konigth.3 $e |- P = X_ i e. A ( N ` i ) $.
    ${
      $d A a e f i $.  $d D a e $.  $d E a i $.  $d M a f $.  $d N a e f $.
      $d P a e f $.  $d S a e f $.
      konigth.4 $e |- D = ( i e. A |-> ( a e. ( M ` i ) |->
        ( ( f ` a ) ` i ) ) ) $.
      konigth.5 $e |- E = ( i e. A |-> ( e ` i ) ) $.
      $( Lemma for ~ konigth . $)
      konigthlem $p |- ( A. i e. A ( M ` i ) ~< ( N ` i ) -> S ~< P ) $=
        ( cv cfv csdm wbr wral wn wfo wex crn cdif c0 wne wcel wa cdom wfn
        cmpt cvv fvex a1i rgen eqid mptfng mpbi wceq mptexg ax-mp fvmpt2 mpan2
        fneq1d mpbiri wi fnrndomg syl domsdomtr sylan sdomdif ralimiaa difss
        ssexi ac6c5 weq equid cixp eldifi eleq1d syl5ibr ralimia jctil eqeltri
        elixp sylibr syl6eleqr wrex foelrn expcom ciun eleq2i eliun bitri
        hbra1 ax-17 hban w3a ad2antrl fveq1 fveq1d sylan9eq eqcomd eqtr3d
        fnfvelrn adantl eqeltrd 3adant1 simp1 simp3l ra4 eldifn syl6 sylc
        pm2.65i pm2.21i 3expia exp3a r19.23ad syl5bi ex com23 r19.23adv syl9r
        mpd mt2i 19.23aiv 3syl nexdv 0dom mpan 0sdom sylib ralimi neeq1i
        ixpexg mp2an ac9 3bitr4i wb iunex domtri biimpri fodomr mp3an1 syl2an
        mtand notnot ) GQZIRZUUKJRZSTZGAUAZDCSTZUBZUBUUPUUOUUQDCFQZUCZFUDZUUOUU
        SFUUOUUMUUKBRZUEZUFZUGUHZGAUAUUKEQZRZUVCUIZGAUAZEUDUUSUBZUUNUVDGAUUKAUI
        ZUUNUJUVBUUMSTZUVDUVJUVBUULUKTZUUNUVKUVJUVAUULULZUVLUVJUVMKUULUUKKQZUUR
        RZRZUMZUULULZUVPUNUIZKUULUAUVRUVSKUULUVSUVNUULUIZUUKUVOUOZUPUQKUULUVPUV
        QUVQURZUSUTUVJUULUVAUVQUVJUVQUNUIZUVAUVQVAUULUNUIZUWCUUKIUOZKUULUVPUNVB
        VCGAUVQUNBOVDVEZVFVGZUWDUVMUVLVHUWEUULUNUVAVIVCVJUVBUULUUMVKVLUVBUUMVMV
        JVNGAUVCELUVCUUMUUKJUOZUUMUVBVOVPVQUVHUVIEUVHUUSFFVRZFVSUVHHCUIZUUSUWIU
        BZVHUVHHGAUUMVTZCUVHHAULZUUKHRZUUMUIZGAUAZUJHUWLUIUVHUWPUWMUVGUWOGAUVGU
        WOUVJUVFUUMUIUVFUUMUVBWAUVJUWNUVFUUMUVJUVFUNUIZUWNUVFVAZUUKUVEUOZGAUVFU
        NHPVDVEZWBWCWDUWQGAUAUWMUWQGAUWQUVJUWSUPUQGAUVFHPUSUTWEGAUUMHHGAUVFUMZU
        NPAUNUIZUXAUNUILGAUVFUNVBVCWFWGWHNWIUWJUUSHUVOVAZKDWJZUVHUWKUUSUWJUXDKD
        CHUURWKWLUVHUXCUWKKDUVHUXCUVNDUIZUWKUVHUXCUXEUWKVHUXEUVTGAWJZUVHUXCUJZU
        WKUXEUVNGAUULWMZUIUXFDUXHUVNMWNGUVNAUULWOWPUXGUVTUWKGAUVHUXCGUVGGAWQUXC
        GWRWSUWKGWRUXGUVJUVTUWKUVHUXCUVJUVTUJZUWKUVHUXCUXIWTZUWKUXJUVFUVBUIZUXC
        UXIUXKUVHUXCUXIUJZUVFUVNUVARZUVBUXLUWNUVFUXMUVJUWRUXCUVTUWTXAUXCUXIUWNU
        VPUXMUUKHUVOXBUXIUXMUVPUVJUVTUXMUVNUVQRZUVPUVJUVNUVAUVQUWFXCUVTUVSUXNUV
        PVAUWAKUULUVPUNUVQUWBVDVEXDXEXDXFUXIUXMUVBUIZUXCUVJUVMUVTUXOUWGUULUVNUV
        AXGVLXHXIXJUXJUVHUVJUXKUBZUVHUXCUXIXKUVHUXCUVJUVTXLUVHUVJUVGUXPUVGGAXMU
        VFUUMUVBXNXOXPXQXRXSXTYAYBYCYDYEYFYGYHYIYJYKUUOUGCSTZCDUKTZUUTUUQUUOUUM
        UGUHZGAUAZUXQUUNUXSGAUUNUGUUMSTZUXSUGUULUKTUUNUYAUULYLUGUULUUMVKYMUUMUW
        HYNYOYPCUGUHUWLUGUHUXQUXTCUWLUGNYQCCUWLUNNUXBUUMUNUIZGAUAUWLUNUILUYBGAU
        YBUVJUWHUPUQGAUUMUNUNYRYSWFZYNGAUUMLUWHYTUUAWHUXRUUQCUNUIDUNUIZUXRUUQUU
        BUYCDUXHUNMGAUULLUWEUUCWFZCDUNUNUUDYSUUEUYDUXQUXRUUTUYEDCUNFUUFUUGUUHUU
        IUUPUUJWH $.
        $( [22-Feb-2013] $)
    $}
    $d A a e f i $.  $d A a e i j $.  $d M a b e f $.  $d N a e f $.
    $d P a e f $.  $d S a e f $.  $d b e f i $.
    $( Konig's Theorem.  If ` m ( i ) ~< n ( i ) ` for all ` i e. A ` , then
       ` sum_ i e. A m ( i ) ~< prod_ i e. A n ( i ) ` , where the sums and
       products stand in for disjoint union and infinite cartesian product.
       The version here is proven with regular unions rather than disjoint
       unions for convenience, but the version with disjoint unions is clearly
       a special case of this version.  The Axiom of Choice is needed for this
       proof, but it contains AC as a simple corrolary (letting
       ` m ( i ) = (/) ` , this theorem says that an infinite cartesian product
       of nonempty sets is nonempty), so this is an AC equivalent.  Theorem
       11.26 of [TakeutiZaring] p. 107.  (Contributed by Mario Carneiro,
       22-Feb-2013.) $)
    konigth $p |- ( A. i e. A ( M ` i ) ~< ( N ` i ) -> S ~< P ) $=
      ( vb vf ve vj va cv cfv cmpt eqid weq fveq2 fveq1d cbvmptv mpteqi
      konigthlem ) ADAJDOZEPZUEJOZKOZPZPZQZQBCLKDMAMOZLOZPZQEFNGHIDAUKANUFUENOZ
      UHPZPZQARJNUFUJUQJNSUEUIUPUGUOUHTUAUBUCMDAUNUEUMPULUEUMTUBUD $.
      $( [22-Feb-2013] $)
  $}

  $( The power set of an aleph dominates the successor aleph.  (The Generalized
     Continuum Hypothesis says they are equinumerous.) $)
  alephsucpw $p |- ( aleph ` suc A ) ~<_ ~P ( aleph ` A ) $=
    ( csuc cale cfv cpw cdom wbr csdm wn fvex canth2 wi wa alephnbtwn2 imnan
    mpbir ax-mp cvv wcel wb pwex domtri mp2an ) ABZCDZACDZEZFGZUGUEHGZIZUFUGHGZ
    UJUFACJZKUKUJLUKUIMIAUGNUKUIOPQUERSUGRSUHUJTUDCJUFULUAUEUGRRUBUCP $.
    $( [27-Aug-2005] $)

  $( The set exponentiation of 2 to the aleph-zero has cardinality of at least
     aleph-one.  (If we were to assume the Continuum Hypothesis, their
     cardinalities would be the same.) $)
  aleph1 $p |- ( aleph ` 1o ) ~<_ ( 2o ^m ( aleph ` (/) ) ) $=
    ( c1o cale cfv c0 csuc c2o cmap co cdom df-1o fveq2i cpw wbr alephsucpw
    cvv wcel cen wb oprex fvex pw2en domen2 mp2an mpbi eqbrtri ) ABCDEZBCZFDBCZ
    GHZIAUFBJKUGUHLZIMZUGUIIMZDNUIOPUJUIQMUKULRFUHGSUHDBTUAUJUIUGOUBUCUDUE $.
    $( [7-Jul-2004] $)

  ${
    $d x y z w A $.
    $( An alternate way to express the value of the aleph function for nonzero
       arguments.  Theorem 64 of [Suppes] p. 229. $)
    alephval2 $p |- ( ( A e. On /\ (/) e. A ) -> ( aleph ` A ) =
                    |^| { x e. On | A. y e. A ( aleph ` y ) ~< x } ) $=
      ( vz con0 wcel c0 wa cale cfv cv csdm wbr wral crab wn cint wceq
      alephordi r19.21aiv alephon jctil breq2 ralbidv elrab sylibr adantr wb
      oneli ccrd cardsdomel alephcard eleq2i syl6rbb syl ibi com cdom wo wi
      cvv omex visset entri3 mp2an wss carddom cardom sseq1i bitr3i wrex
      cardidm cardalephex mpbii alephord ancoms cen cardid sdomen1 breq1
      syl5rbbr sylan9bb fveq2 breq1d rcla4v sdomirr sdomen2 syl5bbr mtbiri
      nsyli com12 adantl sylbird exp31 r19.23adv syl5 syl5bi wne ne0i onelon
      alephgeom ssdomg ax-mp sylbi domtr sylan2 domnsym expr r19.2z ex syl2im
      rexnal syl6ib expimpd a1d com3r jaod mpi simprbi con3i syl6 ssrab2
      oneqmini syl11anc ) CEFZGCFZHZCIJZBKZIJZAKZLMZBCNZAEOZFZDKZYNFZPZDYHNZYHY
      NQRZYEYOYFYEYHEFZYJYHLMZBCNZHYOYEUUCUUAYEUUBBCYICSTCUAZUBYMUUCAYHEYKYHRYL
      UUBBCYKYHYJLUCUDUEUFUGYGYRDYHYGYPYHFZYJYPLMZBCNZPZYRUUEYPYHLMZYGUUHUUEUUI
      UUEYPEFZUUEUUIUHYHYPUUDUIUUJUUIYPYHUJJZFUUEYPYHUKUUKYHYPCULUMUNUOUPYGUQYP
      URMZYPUQURMZUSZUUIUUHUTZUQVAFZYPVAFZUUNVBDVCZUQYPVAVAVDVEYGUULUUOUUMYEUUL
      UUOUTYFUULUQYPUJJZVFZYEUUOUULUQUJJZUUSVFZUUTUUPUUQUVBUULUHVBUURUQYPVAVAVG
      VEUVAUQUUSVHVIVJUUTUUSYKIJZRZAEVKZYEUUOUUTUUSUJJUUSRUVEYPVLAUUSVMVNYEUVDU
      UOAEYEYKEFZUVDUUOYEUVFHZUVDHUUIYKCFZUUHUVGUVHUVCYHLMZUVDUUIUVFYEUVHUVIUHY
      KCVOVPUUIUUSYHLMZUVDUVIUUQUUSYPVQMZUVJUUIUHUURYPVRZUUSYPYHVAVSVEUUSUVCYHL
      VTWAWBUVDUVHUUHUTUVGUVHUVDUUHUVHUUGUVCYPLMZUVDUUFUVMBYKCYIYKRYJUVCYPLYIYK
      IWCWDWEUVDUVMUVCUVCLMZUVCWFUVMUVCUUSLMZUVDUVNUUQUVKUVOUVMUHUURUVLUUSYPUVC
      VAWGVEUUSUVCUVCLUCWHWIWJWKWLWMWNWOWPWQUGUUMUUIYGUUHUUMYGUUHUTUUIUUMYEYFUU
      HYFUUMYEHZUUHYFUVPUUFPZBCVKZUUHYFCGWRZUVPUVQBCNZUVRCGWSUVPUVQBCUUMYEYICFZ
      UVQYEUWAHUUMYIEFZUVQCYIWTUUMUWBHYPYJURMZUVQUWBUUMUQYJURMZUWCUWBUQYJVFZUWD
      YIXAUUPUWEUWDUTVBUQYJVAXBXCXDYPUQYJXEXFYPYJXGUOXFXHTUVSUVTUVRUVQBCXIXJXKU
      UFBCXLXMWKXNXOXPXQXRWPYQUUGYQUUJUUGYMUUGAYPEYKYPRYLUUFBCYKYPYJLUCUDUEXSXT
      YATYNEVFYOYSHYTUTYMAEYBDYHYNYCXCYD $.
      $( [15-Nov-2003] $)
  $}

  ${
    $d x y z w A $.
    dominfac.1 $e |- A e. _V $.
    $( A nonempty set that is a subset of its union is infinite.  This version
       is proved from ~ ax-ac .  See ~ dominf for a version proved from
       ~ ax-cc . $)
    dominfac $p |- ( ( A =/= (/) /\ A C_ U. A ) -> om ~<_ A ) $=
      ( vx vz vw vy cv c0 wne cuni wss wa com cdom wbr wi wceq neeq1 id unieq
      sseq12d anbi12d breq2 imbi12d cpw cin crab copab crdg cres wf1 eqid
      inf3lem6 cvv wcel visset pwex f1dom2g ax-mp csdm wn cfn pwfi biimpi
      isfinite 3imtr4i con3i wb omex domtri mp2an 3syl vtocl ) CGZHIZVNVNJZKZLZ
      MVNNOZPAHIZAAJZKZLZMANOZPCABVNAQZVRWCVSWDWEVOVTVQWBVNAHRWEVNAVPWAWESVNATU
      AUBVNAMNUCUDVRMVNUEZDGEGVNUFFGKEVNUGQFDUHZHUIMUJZUKZMWFNOZVSCFDEAAWHWGWGU
      LWHULBBUMWFUNUOZWIWJPVNCUPZUQZMWFUNWHURUSWFMUTOZVAZVNMUTOZVAZWJVSWPWNVNVB
      UOZWFVBUOZWPWNWRWSVNVCVDVNVEWFVEVFVGMUNUOZWKWJWOVHVIWMMWFUNUNVJVKWTVNUNUO
      VSWQVHVIWLMVNUNUNVJVKVFVLVM $.
      $( [25-Mar-2007] $)
  $}


