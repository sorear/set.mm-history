$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Intermediate value theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d a b c A $.  $d b c B $.  $d c C $.  $d a b c F $.  $d a b c S $.
    pmltpclem1.1 $e |- ( ph -> A e. S ) $.
    pmltpclem1.2 $e |- ( ph -> B e. S ) $.
    pmltpclem1.3 $e |- ( ph -> C e. S ) $.
    pmltpclem1.4 $e |- ( ph -> A < B ) $.
    pmltpclem1.5 $e |- ( ph -> B < C ) $.
    pmltpclem1.6 $e |- ( ph ->
      ( ( ( F ` A ) < ( F ` B ) /\ ( F ` C ) < ( F ` B ) ) \/
        ( ( F ` B ) < ( F ` A ) /\ ( F ` B ) < ( F ` C ) ) ) ) $.
    $( Lemma for ~ pmltpc . $)
    pmltpclem1 $p |- ( ph ->
      E. a e. S E. b e. S E. c e. S ( a < b /\ b < c /\
        ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
          ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) $=
      ( clt wbr cfv wa wo wcel w3a wrex wceq breq1 breq1d anbi1d breq2d orbi12d
      fveq2 3anbi13d breq2 anbi12d 3anbi123d anbi2d 3anbi23d rcla43ev syl33anc
      cv ) ABEUACEUADEUABCPQZCDPQZBFRZCFRZPQZDFRZVCPQZSZVCVBPQZVCVEPQZSZTZGUSZH
      USZPQZVMIUSZPQZVLFRZVMFRZPQZVOFRZVRPQZSZVRVQPQZVRVTPQZSZTZUBZIEUCHEUCGEUC
      JKLMNOWGUTVAVKUBBVMPQZVPVBVRPQZWASZVRVBPQZWDSZTZUBUTCVOPQZVDVTVCPQZSZVHVC
      VTPQZSZTZUBGHIBCDEEEVLBUDZVNWHWFWMVPVLBVMPUEWTWBWJWEWLWTVSWIWAWTVQVBVRPVL
      BFUJZUFUGWTWCWKWDWTVQVBVRPXAUHUGUIUKVMCUDZWHUTVPWNWMWSVMCBPULVMCVOPUEXBWJ
      WPWLWRXBWIVDWAWOXBVRVCVBPVMCFUJZUHXBVRVCVTPXCUHUMXBWKVHWDWQXBVRVCVBPXCUFX
      BVRVCVTPXCUFUMUIUNVODUDZWNVAWSVKUTVODCPULXDWPVGWRVJXDWOVFVDXDVTVEVCPVODFU
      JZUFUOXDWQVIVHXDVTVEVCPXEUHUOUIUPUQUR $.
      $( [1-Jul-2014] $)
  $}

  ${
    $d a b c w x y z A $.  $d a b c w x y z F $.  $d w x y z ph $.  $d b c V $.
    $d a b c U $.  $d a b c W $.  $d b c X $.
    pmltpc.1 $e |- ( ph -> F e. ( RR ^pm RR ) ) $.
    pmltpc.2 $e |- ( ph -> A C_ dom F ) $.
    pmltpc.3 $e |- ( ph -> U e. A ) $.
    pmltpc.4 $e |- ( ph -> V e. A ) $.
    pmltpc.5 $e |- ( ph -> W e. A ) $.
    pmltpc.6 $e |- ( ph -> X e. A ) $.
    pmltpc.7 $e |- ( ph -> U <_ V ) $.
    pmltpc.8 $e |- ( ph -> W <_ X ) $.
    pmltpc.9 $e |- ( ph -> -. ( F ` U ) <_ ( F ` V ) ) $.
    pmltpc.10 $e |- ( ph -> -. ( F ` X ) <_ ( F ` W ) ) $.
    $( Lemma for ~ pmltpc . $)
    pmltpclem2 $p |- ( ph -> E. a e. A E. b e. A E. c e. A ( a < b /\ b < c /\
      ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
        ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) $=
      ( cv clt wbr cfv wa wo w3a wrex wcel ad2antrr simpr wne cr cdm wf wss cpm
      co reex elpm2 sylib simpld sseldd ffvelrn syl2anc wn wb ltnle mpbird ltne
      cle syl3anc wceq fveq2 eqcomd necon3i syl simprd leltne simplr pmltpclem1
      jca orcd olcd adantr pm2.61ltlei lelttrd lttrd ) AHUAZIUAZUBUCWJJUAZUBUCW
      IDUDZWJDUDZUBUCWKDUDZWMUBUCUEWMWLUBUCWMWNUBUCUEUFUGJBUHIBUHHBUHZFDUDZCDUD
      ZAWPWQUBUCZUEZWOFCWSFCUBUCZUEZFCEBDHIJAFBUIZWRWTOUJACBUIZWRWTMUJAEBUIZWRW
      TNUJWSWTUKACEUBUCZWRWTAXEECULZAWQEDUDZULZXFAXGUMUIZWQUMUIZXGWQUBUCZXHADUN
      ZUMDUOZEXLUIXIAXMXLUMUPZADUMUMUQURUIXMXNUEKUMUMDUSUSUTVAZVBZABXLELNVCZXLU
      MEDVDVEZAXMCXLUIXJXPABXLCLMVCZXLUMCDVDVEZAXKWQXGVKUCVFZSAXIXJXKYAVGXRXTXG
      WQVHVEVIZXGWQVJVLECWQXGECVMXGWQECDVNVOVPVQACUMUIZEUMUIZCEVKUCXEXFVGAXLUMC
      AXMXNXOVRZXSVCZAXLUMEYEXQVCZQCEVSVLVIZUJXAWRXKUEWQWPUBUCZWQXGUBUCZUEXAWRX
      KAWRWTVTAXKWRWTYBUJWBWCWAWSCFVKUCZUEZCFGBDHIJAXCWRYKMUJAXBWRYKOUJAGBUIZWR
      YKPUJYLCFUBUCZFCULZYLWQWPULZYOYLWPUMUIZXJWRYPAYQWRYKAXMFXLUIYQXPABXLFLOVC
      ZXLUMFDVDVEZUJAXJWRYKXTUJAWRYKVTZWPWQVJVLFCWQWPFCVMWPWQFCDVNVOVPVQYLYCFUM
      UIZYKYNYOVGAYCWRYKYFUJAUUAWRYKAXLUMFYEYRVCZUJWSYKUKCFVSVLVIAFGUBUCZWRYKAU
      UCGFULZAGDUDZWPULZUUDAYQUUEUMUIZWPUUEUBUCZUUFYSAXMGXLUIUUGXPABXLGLPVCZXLU
      MGDVDVEZAUUHUUEWPVKUCVFZTAYQUUGUUHUUKVGYSUUJWPUUEVHVEVIZWPUUEVJVLGFUUEWPG
      FDVNVPVQAUUAGUMUIZFGVKUCUUCUUDVGUUBAXLUMGYEUUIVCZRFGVSVLVIZUJYLWRUUHUEYIU
      UEWPUBUCZUEYLWRUUHYTAUUHWRYKUULUJWBWDWAAUUAWRUUBWEAYCWRYFWEWFAWQWPVKUCZUE
      ZWOEGUUREGUBUCZUEZCEGBDHIJAXCUUQUUSMUJAXDUUQUUSNUJAYMUUQUUSPUJAXEUUQUUSYH
      UJUURUUSUKUUTXKXGUUEUBUCZUEYJUUEXGUBUCZUEUUTXKUVAAXKUUQUUSYBUJUURUVAUUSUU
      RXGWQUUEAXIUUQXRWEZAXJUUQXTWEZAUUGUUQUUJWEZAXKUUQYBWEUURWQWPUUEUVDAYQUUQY
      SWEUVEAUUQUKAUUHUUQUULWEWGWHZWEWBWDWAUURGEVKUCZUEZFGEBDHIJAXBUUQUVGOUJAYM
      UUQUVGPUJAXDUUQUVGNUJAUUCUUQUVGUUOUJUVHGEUBUCZEGULZUVHUUEXGULZUVJUVHXIUUG
      UVAUVKUURXIUVGUVCWEUURUUGUVGUVEWEUURUVAUVGUVFWEZXGUUEVJVLEGUUEXGEGVMXGUUE
      EGDVNVOVPVQUVHUUMYDUVGUVIUVJVGAUUMUUQUVGUUNUJAYDUUQUVGYGUJUURUVGUKGEVSVLV
      IUVHUUHUVAUEUUPUVBUEUVHUUHUVAAUUHUUQUVGUULUJUVLWBWCWAAYDUUQYGWEAUUMUUQUUN
      WEWFYSXTWF $.
      $( [1-Jul-2014] $)
  $}

  ${
    $d a b c w x y z A $.  $d a b c w x y z F $.
    $( Any function on the reals is either increasing, decreasing, or has a
       triple of points in a vee formation.  (This theorem was created on
       demand by Mario Carneiro for the 6PCM conference in Bialystok,
       1-Jul-2014.) $)
    pmltpc $p |- ( ( F e. ( RR ^pm RR ) /\ A C_ dom F ) ->
      ( A. x e. A A. y e. A ( x <_ y -> ( F ` x ) <_ ( F ` y ) ) \/
        A. x e. A A. y e. A ( x <_ y -> ( F ` y ) <_ ( F ` x ) ) \/
        E. a e. A E. b e. A E. c e. A ( a < b /\ b < c /\
        ( ( ( F ` a ) < ( F ` b ) /\ ( F ` c ) < ( F ` b ) ) \/
          ( ( F ` b ) < ( F ` a ) /\ ( F ` b ) < ( F ` c ) ) ) ) ) ) $=
      ( vz vw wcel wa cv cle wbr cfv wi wral clt wrex wn cr cpm cdm wss w3a w3o
      co wo rexanali rexbii rexnal bitri wceq breq1 fveq2 breq2d imbi12d breq1d
      breq2 cbvral2v notbii 3bitri anbi12i reeanv 3bitr4i simplll simpld simprd
      simpllr simplrl simplrr simprll simprrl simprlr simprrr pmltpclem2 df-3or
      ioran ex rexlimdvva syl5bir orrd sylibr ) DUAUAUBUGJZCDUCUDZKZALZBLZMNZWG
      DOZWHDOZMNZPBCQZACQZWIWKWJMNZPZBCQACQZUHZELZFLZRNWTGLZRNWSDOZWTDOZRNXADOZ
      XCRNKXCXBRNXCXDRNKUHUEGCSFCSECSZUHWNWQXEUFWFWRXEWRTZWIWLTZKZBCSZHLZILZMNZ
      XKDOZXJDOZMNZTZKZICSZKZHCSACSZWFXEXIACSZXRHCSZKWNTZWQTZKXTXFYAYCYBYDYAWMT
      ZACSYCXIYEACWIWLBCUIUJWMACUKULYBXLXOPZICQZTZHCSYGHCQZTYDXRYHHCXLXOICUIUJY
      GHCUKYIWQYFWPWGXKMNZXMWJMNZPHIABCCXJWGUMZXLYJXOYKXJWGXKMUNYLXNWJXMMXJWGDU
      OUPUQXKWHUMZYJWIYKWOXKWHWGMUSYMXMWKWJMXKWHDUOURUQUTVAVBVCXIXRAHCCVDWNWQVR
      VEWFXSXEAHCCXSXHXQKZICSBCSWFWGCJZXJCJZKZKZXEXHXQBICCVDYRYNXEBICCYRWHCJZXK
      CJZKZKZYNXEUUBYNKZCWGDWHXJXKEFGUUCWDWEWFYQUUAYNVFZVGUUCWDWEUUDVHUUCYOYPWF
      YQUUAYNVIZVGYRYSYTYNVJUUCYOYPUUEVHYRYSYTYNVKUUBWIXGXQVLUUBXHXLXPVMUUBWIXG
      XQVNUUBXHXLXPVOVPVSVTWAVTWAWBWNWQXEVQWC $.
      $( [1-Jul-2014] $)
  $}

  ${
    $d c x y z B $.  $d c x y z D $.  $d c x y z F $.  $d c x y z ph $.
    $d c x y A $.  $d x y z C $.  $d x y z S $.  $d c x y z U $.
    ivth.1 $e |- ( ph -> A e. RR ) $.
    ivth.2 $e |- ( ph -> B e. RR ) $.
    ivth.3 $e |- ( ph -> U e. RR ) $.
    ivth.4 $e |- ( ph -> A < B ) $.
    ivth.5 $e |- ( ph -> ( A [,] B ) C_ D ) $.
    ivth.7 $e |- ( ph -> F e. ( D -cn-> CC ) ) $.
    ivth.8 $e |- ( ( ph /\ x e. ( A [,] B ) ) -> ( F ` x ) e. RR ) $.
    ${
      ivth.9 $e |- ( ph -> ( ( F ` A ) < U /\ U < ( F ` B ) ) ) $.
      ${
        ivth.10 $e |- S = { x e. ( A [,] B ) | ( F ` x ) <_ U } $.
        $( Lemma for ~ ivth .  The set ` S ` of all ` x ` values with
           ` ( F `` x ) ` less than ` U ` is lower bounded by ` A ` and upper
           bounded by ` B ` . $)
        ivthlem1 $p |- ( ph -> ( A e. S /\ A. z e. S z <_ B ) ) $=
          ( wcel wbr cv cle wral cicc co cfv cr clt wi ltle syl2anc mpd syl3anc
          lbicc2 simpld ralrimiva wceq fveq2 eleq1d rcla4v sylc breq1d sylanbrc
          elrab2 crab ssrab2 eqsstri sseli cxr rexr syl iccleub 3expia ralrimiv
          syl5 jca ) ADGSZCUAZEUBTZCGUCADDEUDUEZSZDIUFZHUBTZVQADUGSZEUGSZDEUBTZ
          WAJKADEUHTZWFMAWDWEWGWFUIJKDEUJUKULDEUNUMZAWBHUHTZWCAWIHEIUFUHTQUOAWB
          UGSZHUGSWIWCUIAWABUAZIUFZUGSZBVTUCWJWHAWMBVTPUPWMWJBDVTWKDUQZWLWBUGWK
          DIURZUSUTVALWBHUJUKULWLHUBTZWCBDVTGWNWLWBHUBWOVBRVDVCAVSCGVRGSVRVTSZA
          VSGVTVRGWPBVTVEVTRWPBVTVFVGVHADVISZEVISZWQVSUIAWDWRJDVJVKAWEWSKEVJVKW
          RWSWQVSDEVRVLVMUKVOVNVP $.
          $( [17-Jun-2014] $)

        ivth.11 $e |- C = sup ( S , RR , < ) $.
        $( Lemma for ~ ivth .  Show that the supremum of ` S ` cannot be less
           than ` U ` .  If it was, continuity of ` F ` implies that there are
           points just above the supremum that are also less than ` U ` , a
           contradiction. $)
        ivthlem2 $p |- ( ph -> -. ( F ` C ) < U ) $=
          ( wbr vy vz cfv clt wn wa cv cmin co cabs wi wral crp wrex ccncf wcel
          cc wss ssid a1i adantr cicc cr cle w3a wb elicc2 syl2anc csup c0 crab
          wne ssrab2 eqsstri iccssre syl5ss ivthlem1 simpld ne0i syl wceq breq2
          simprd ralbidv rcla4ev 3jca suprcl syl5eqel suprub syl6breqr suprleub
          syl22anc syl5eqbr mpbir3and sseldd ralrimiva fveq2 eleq1d rcla4v sylc
          difrp biimpa cncfi ssralv ad2antrr c2 cdiv caddc rphalfcl adantl rpre
          cif readdcl ifcl ltle mpd ubicc2 syl3anc mpan2d imp breq2d syl5ibrcom
          lttr ltnr notbid necon2ad jctild ltlen sylibrd ifboth letrd abssubge0
          ltaddrp min1 breq1d recnd ex syld syl5 rexlimdva min2 rphalflt ltadd2
          mpbid lelttrd ltsubadd2 mpbird eqbrtrd fveq2d anbi12d syl12anc r19.29
          oveq1 pm3.45 simprr simprl simplll resubcl pncan3 elrab2 baib 3imtr4d
          absdiflt ad2antrl lenlt sylibd adantld sylbid mt2d pm2.21d expr com23
          imp3a pm2.01d ) AEIUCZHUDTZAUVPUVPUEZAUVPUFZUAUGZEUHUIZUJUCZUBUGZUDTZ
          UVSIUCZUVOUHUIUJUCHUVOUHUIZUDTZUKZUAFULZUBUMUNZUVQUVRUQUQURZIFUQUOUIU
          PZEFUPZUWEUMUPZUWIUWJUVRUQUSUTAUWKUVPOVAAUWLUVPACDVBUIZFENAEUWNUPZEVC
          UPZCEVDTZEDVDTZACVCUPZDVCUPZUWOUWPUWQUWRVEVFJKCDEVGVHAEGVCUDVIZVCSAGV
          CURZGVJVLZUWBBUGZVDTZUBGULZBVCUNZVEZUXAVCUPAUXBUXCUXGAGUWNVCGUXDIUCZH
          VDTZBUWNVKUWNRUXJBUWNVMVNAUWSUWTUWNVCURZJKCDVOVHZVPZACGUPZUXCAUXNUWBD
          VDTZUBGULZABUBCDFGHIJKLMNOPQRVQZVRZGCVSVTZAUWTUXPUXGKAUXNUXPUXQWCZUXF
          UXPBDVCUXDDWAZUXEUXOUBGUXDDUWBVDWBWDWEVHWFZBUBGWGVTWHZACUXAEVDAUXHUXN
          CUXAVDTUYBUXRBUBGCWIVHSWJZAEUXADVDSAUXBUXCUWTUXPUXADVDTUXMUXSKUXTUBGD
          WKWLWMZWNZWOVAAUVPUWMAUVOVCUPZHVCUPZUVPUWMVFAUWOUXIVCUPZBUWNULZUYGUYF
          AUYIBUWNPWPZUYIUYGBEUWNUXDEWAUXIUVOVCUXDEIWQWRWSWTZLUVOHXAVHXBUBUAFUQ
          EUWEIXCWLUVRUWHUVQUBUMUVRUWBUMUPZUFZUWHUWGUAUWNULZUVQAUWHUYOUKZUVPUYM
          AUWNFURUYPNUWGUAUWNFXDVTXEUYNUYOUWCEUVSUDTZUFZUAUWNUNZUVQUYNDEUWBXFXG
          UIZXHUIZVDTZDVUAXLZUWNUPZVUCEUHUIZUJUCZUWBUDTZEVUCUDTZUYSUYNVUDVUCVCU
          PZCVUCVDTZVUCDVDTZAVUDVUIVUJVUKVEVFZUVPUYMAUWSUWTVULJKCDVUCVGVHXEUYNU
          WTVUAVCUPZVUIAUWTUVPUYMKXEZUYNUWPUYTVCUPZVUMAUWPUVPUYMUYCXEZUYNUYTUMU
          PZVUOUYMVUQUVRUWBXIXJZUYTXKVTZEUYTXMVHZVUBDVUAVCXNVHZUYNCEVUCAUWSUVPU
          YMJXEVUPVVAAUWQUVPUYMUYDXEUYNVUHEVUCVDTZUYNEDUDTZEVUAUDTZVUHUYNUVODIU
          CZUDTZVVCUVRVVFUYMAUVPVVFAUVPHVVEUDTZVVFACIUCHUDTVVGQWCAUYGUYHVVEVCUP
          ZUVPVVGUFVVFUKUYLLADUWNUPZUYJVVHAUWSUWTCDVDTZVVIJKACDUDTZVVJMAUWSUWTV
          VKVVJUKJKCDXOVHXPCDXQXRUYKUYIVVHBDUWNUYAUXIVVEVCUXDDIWQWRWSWTUVOHVVEY
          CXRXSXTVAAVVFVVCUKUVPUYMAVVFUWRDEVLZUFZVVCAVVFVVLUWRAVVFDEAVVFUEDEWAZ
          UVOUVOUDTZUEZAUYGVVPUYLUVOYDVTVVNVVFVVOVVNVVEUVOUVOUDDEIWQYAYEYBYFUYE
          YGAUWPUWTVVCVVMVFUYCKEDYHVHYIXEXPUYNUWPVUQVVDVUPVUREUYTYMVHVUBVVCVVDV
          UHDVUADVUCEUDWBVUAVUCEUDWBYJVHZUYNUWPVUIVUHVVBUKVUPVVAEVUCXOVHXPZYKUY
          NUWTVUMVUKVUNVUTDVUAYNVHWNUYNVUFVUEUWBUDUYNUWPVUIVVBVUFVUEWAVUPVVAVVR
          EVUCYLXRUYNVUEUWBUDTZVUCEUWBXHUIZUDTZUYNVUCVUAVVTVVAVUTUYNUWPUWBVCUPZ
          VVTVCUPVUPUYMVWBUVRUWBXKXJZEUWBXMVHUYNUWTVUMVUCVUAVDTVUNVUTDVUAUUAVHU
          YNUYTUWBUDTZVUAVVTUDTZUYMVWDUVRUWBUUBXJUYNVUOVWBUWPVWDVWEVFVUSVWCVUPU
          YTUWBEUUCXRUUDUUEUYNVUIUWPVWBVVSVWAVFVVAVUPVWCVUCEUWBUUFXRUUGUUHVVQUY
          RVUGVUHUFUAVUCUWNUVSVUCWAZUWCVUGUYQVUHVWFUWAVUFUWBUDVWFUVTVUEUJUVSVUC
          EUHUUMUUIYOUVSVUCEUDWBUUJWEUUKUYOUYSUFUWGUYRUFZUAUWNUNUYNUVQUWGUYRUAU
          WNUULUYNVWGUVQUAUWNVWGUWFUYQUFZUYNUVSUWNUPZUFZUVQUWGUYRVWHUWCUWFUYQUU
          NXTVWJUWFUYQUVQVWJUYQUWFUVQUYNVWIUYQUWFUVQUKUYNVWIUYQUFZUFZUWFUVQVWLU
          WFUYQUYNVWIUYQUUOVWLUWFUVOUWEUHUIUWDUDTZUWDUVOUWEXHUIZUDTZUFZUYQUEZVW
          LUWDVCUPZUYGUWEVCUPZUWFVWPVFVWLVWIUYJVWRUYNVWIUYQUUPZVWLAUYJAUVPUYMVW
          KUUQZUYKVTUYIVWRBUVSUWNUXDUVSWAZUXIUWDVCUXDUVSIWQZWRWSWTZVWLAUYGVXAUY
          LVTZVWLUYHUYGVWSVWLAUYHVXALVTZVXEHUVOUURVHUWDUVOUWEUVCXRVWLVWOVWQVWMV
          WLVWOUVSGUPZVWQVWLUWDHUDTZUWDHVDTZVWOVXGVWLVWRUYHVXHVXIUKVXDVXFUWDHXO
          VHVWLVWNHUWDUDVWLUVOUQUPHUQUPVWNHWAVWLUVOVXEYPVWLHVXFYPUVOHUUSVHYAVWI
          VXGVXIVFUYNUYQVXGVWIVXIUXJVXIBUVSUWNGVXBUXIUWDHVDVXCYORUUTUVAUVDUVBVW
          LVXGUVSEVDTZVWQVWLAVXGVXJUKZVXAAUXHVXKUYBUXHVXGVXJUXHVXGUFUVSUXAEVDBU
          BGUVSWISWJYQVTVTVWLUVSVCUPUWPVXJVWQVFVWLUWNVCUVSVWLAUXKVXAUXLVTVWTWOV
          WLAUWPVXAUYCVTUVSEUVEVHUVFYRUVGUVHUVIUVJUVKUVLUVMYSYTYSXSYRYTXPYQUVN
          $.
          $( [17-Jun-2014] $)

        $( Lemma for ~ ivth , the intermediate value theorem.  Show that
           ` ( F `` C ) ` cannot be greater than ` U ` , and so establish the
           existence of a root of the function. $)
        ivthlem3 $p |- ( ph -> ( C e. ( A (,) B ) /\ ( F ` C ) = U ) ) $=
          ( wbr vz vy cioo co wcel cfv wceq clt cxr w3a rexr syl elioo2 syl2anc
          cr wb csup wss c0 wne cv wral wrex cicc ssrab2 eqsstri iccssre syl5ss
          cle crab ivthlem1 simpld simprd breq2 ralbidv rcla4ev suprcl syl5eqel
          ne0i 3jca wn wa elicc2 syl6breqr suprleub syl22anc syl5eqbr mpbir3and
          suprub ralrimiva fveq2 eleq1d rcla4v sylc lttri3 ivthlem2 cmin wi crp
          cabs cc ccncf ssid a1i adantr sseldd difrp biimpa cncfi ad2antrr rpre
          ssralv adantl resubcl ltsubrp sylancom syl6breq suprlub sseli simplll
          syl12anc ad2antrl simprl abssuble0 syl3anc mpbid eqbrtrd mpd ad2antll
          ex recnd breq1d sylbid rexlimdva notbid necon2ad jctild ltlen sylibrd
          syl5 simprr ltsub23 jca32 reximdv2 r19.29 pm3.45 caddc absdiflt nncan
          imp elrab2 simprbi lenlt pm2.21d adantrd expr com23 imp3a mpan2d syld
          pm2.01d mpbir2and breqtrrd ltnr syl5ibcom breq2d syl5ibrcom jca ) AEC
          DUCUDUEZEIUFZHUGZAUVIEUOUEZCEUHTZEDUHTZACUIUEZDUIUEZUVIUVLUVMUVNUJUPA
          CUOUEZUVOJCUKULADUOUEZUVPKDUKULCDEUMUNAEGUOUHUQZUOSAGUOURZGUSUTZUAVAZ
          BVAZVITZUAGVBZBUOVCZUJZUVSUOUEAUVTUWAUWFAGCDVDUDZUOGUWCIUFZHVITZBUWHV
          JUWHRUWJBUWHVEVFZAUVQUVRUWHUOURZJKCDVGUNZVHZACGUEZUWAAUWOUWBDVITZUAGV
          BZABUACDFGHIJKLMNOPQRVKZVLZGCVSULZAUVRUWQUWFKAUWOUWQUWRVMZUWEUWQBDUOU
          WCDUGUWDUWPUAGUWCDUWBVIVNVOVPUNVTZBUAGVQULVRZACIUFZUVJUHTZUVMAUXDHUVJ
          UHAUXDHUHTZHDIUFZUHTZQVLAUVKUVJHUHTWAZHUVJUHTZWAZAUVJUOUEZHUOUEZUVKUX
          IUXKWBUPAEUWHUEZUWIUOUEZBUWHVBZUXLAUXNUVLCEVITZEDVITZAUVQUVRUXNUVLUXQ
          UXRUJUPJKCDEWCUNUXCACUVSEVIAUWGUWOCUVSVITUXBUWSBUAGCWIUNSWDZAEUVSDVIS
          AUVTUWAUVRUWQUVSDVITUWNUWTKUXAUAGDWEWFWGZWHZAUXOBUWHPWJZUXOUXLBEUWHUW
          CEUGUWIUVJUOUWCEIWKWLWMWNZLUVJHWOUNABCDEFGHIJKLMNOPQRSWPAUXJAUXJUXKAU
          XJWBZUBVAZEWQUDWTUFZUWBUHTZUYEIUFZUVJWQUDWTUFUVJHWQUDZUHTZWRZUBFVBZUA
          WSVCZUXKUYDXAXAURZIFXAXBUDUEZEFUEZUYIWSUEZUYMUYNUYDXAXCXDAUYOUXJOXEAU
          YPUXJAUWHFENUYAXFXEAUXJUYQAUXMUXLUXJUYQUPLUYCHUVJXGUNXHUAUBFXAEUYIIXI
          WFUYDUYLUXKUAWSUYDUWBWSUEZWBZUYLUYKUBUWHVBZUXKAUYLUYTWRZUXJUYRAUWHFUR
          VUANUYKUBUWHFXLULXJUYSUYTUYGUYEGUEZWBZUBUWHVCZUXKUYSEUWBWQUDZUYEUHTZU
          BGVCZVUDUYSUWGVUEUOUEZVUEUVSUHTVUGAUWGUXJUYRUXBXJUYSUVLUWBUOUEZVUHAUV
          LUXJUYRUXCXJZUYRVUIUYDUWBXKXMZEUWBXNUNUYSVUEEUVSUHUYDUYRUVLVUEEUHTVUJ
          EUWBXOXPSXQBUAUBGVUEXRYAUYSVUFVUCUBGUWHUYSVUBVUFWBZUYEUWHUEZVUCWBUYSV
          ULWBZVUMUYGVUBVUBVUMUYSVUFGUWHUYEUWKXSZYBZVUNUYFEUYEWQUDZUWBUHVUNUYEU
          OUEZUVLUYEEVITUYFVUQUGVUNUWHUOUYEVUNAUWLAUXJUYRVULXTZUWMULVUPXFZVUNAU
          VLVUSUXCULZVUNUYEUVSEVIVUNUWGVUBUYEUVSVITVUNAUWGVUSUXBULUYSVUBVUFYCZB
          UAGUYEWIUNSWDUYEEYDYEVUNVUFVUQUWBUHTZUYSVUBVUFUUAVUNUVLVUIVURVUFVVCUP
          VVAUYSVUIVULVUKXEVUTEUWBUYEUUBYEYFYGVVBUUCYJUUDYHUYTVUDWBUYKVUCWBZUBU
          WHVCUYSUXKUYKVUCUBUWHUUEUYSVVDUXKUBUWHVVDUYJVUBWBZUYSVUMWBUXKUYKVUCVV
          EUYGUYJVUBUUFUUJUYSVVEUXKWRVUMUYSUYJVUBUXKUYSVUBUYJUXKUYDUYRVUBUYJUXK
          WRUYDUYRVUBWBZWBZUYJUVJUYIWQUDZUYHUHTZUYHUVJUYIUUGUDUHTZWBZUXKVVGUYHU
          OUEZUXLUYIUOUEZUYJVVKUPVVGVUMUXPVVLVUBVUMUYDUYRVUOYIAUXPUXJVVFUYBXJUX
          OVVLBUYEUWHUWCUYEUGZUWIUYHUOUWCUYEIWKZWLWMWNZAUXLUXJVVFUYCXJZVVGUXLUX
          MVVMVVQAUXMUXJVVFLXJZUVJHXNUNUYHUVJUYIUUHYEVVGVVIUXKVVJVVGVVIHUYHUHTZ
          UXKVVGVVHHUYHUHVVGUVJXAUEHXAUEVVHHUGVVGUVJVVQYKVVGHVVRYKUVJHUUIUNYLVV
          GVVSUXKVVGUYHHVITZVVSWAZVUBVVTUYDUYRVUBVUMVVTUWJVVTBUYEUWHGVVNUWIUYHH
          VIVVOYLRUUKUULYIVVGVVLUXMVVTVWAUPVVPVVRUYHHUUMUNYFUUNYMUUOYMUUPUUQUUR
          XEYTYNYTUUSUUTYNYHYJUVAUVBZUVCAUXEUXQECUTZWBZUVMAUXEVWCUXQAUXEECAUVJU
          VJUHTZWAZECUGZUXEWAAUXLVWFUYCUVJUVDULZVWGVWEUXEVWGUVJUXDUVJUHECIWKYLY
          OUVEYPUXSYQAUVQUVLUVMVWDUPJUXCCEYRUNYSYHAUVJUXGUHTZUVNAUVJHUXGUHVWBAU
          XFUXHQVMYGAVWIUXRDEUTZWBZUVNAVWIVWJUXRAVWIDEAVWIWADEUGZVWFVWHVWLVWIVW
          EVWLUXGUVJUVJUHDEIWKUVFYOUVGYPUXTYQAUVLUVRUVNVWKUPUXCKEDYRUNYSYHWHVWB
          UVH $.
          $( [17-Jun-2014] $) $( [30-Apr-2014] $)
      $}

      $( The intermediate value theorem, increasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (The proof was shortened by Mario Carneiro,
         30-Apr-2014.) $)
      ivth $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( vy cv cfv wceq cle wbr cicc co crab cr clt csup cioo wcel wrex breq1d
        wa fveq2 cbvrabv eqid ivthlem3 eqeq1d rcla4ev syl ) AQRZGSZFUAUBZQCDUCU
        DZUEZUFUGUHZCDUIUDZUJVFGSZFTZUMHRZGSZFTZHVGUKABCDVFEVEFGIJKLMNOPVCBRZGS
        ZFUAUBQBVDVAVMTVBVNFUAVAVMGUNULUOVFUPUQVLVIHVFVGVJVFTVKVHFVJVFGUNURUSUT
        $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ${
      ivth2.9 $e |- ( ph -> ( ( F ` B ) < U /\ U < ( F ` A ) ) ) $.
      $( The intermediate value theorem, decreasing case.  (Contributed by Paul
         Chapman, 22-Jan-2008.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
      ivth2 $p |- ( ph -> E. c e. ( A (,) B ) ( F ` c ) = U ) $=
        ( cfv wceq cr wcel vy cv cneg cmpt cioo co wrex renegcl syl cc negfcncf
        ccncf eqid wa sseld imp fveq2 negeqd negex fvmpt eqeltrd clt wbr cle wi
        cicc ltle syl2anc lbicc2 syl3anc sseldd simprd wb wral ralrimiva eleq1d
        mpd rcla4v sylc ltneg mpbid eqbrtrd simpld ubicc2 breqtrrd jca ioossicc
        ivth syl5ss eqeq1d wf wss ssid cncff sylancr ffvelrn sylan syldan recnd
        adantr neg11 bitrd rexbidva ) AHUBZUAEUAUBZGQZUCZUDZQZFUCZRZHCDUEUFZUGX
        DGQZFRZHXLUGABCDEXJXHHIJAFSTZXJSTKFUHUILMAGEUJULUFZTZXHXPTNUAEGXHXHUMZU
        KUIABUBZCDVFUFZTZUNZXSXHQZXSGQZUCZSYBXSETZYCYERAYAYFAXTEXSMUOUPUAXSXGYE
        EXHXEXSRXFYDXEXSGUQURXRYDUSUTUIYBYDSTZYESTOYDUHUIVAACXHQZXJVBVCXJDXHQZV
        BVCAYHCGQZUCZXJVBACETYHYKRAXTECMACSTZDSTZCDVDVCZCXTTZIJACDVBVCZYNLAYLYM
        YPYNVEIJCDVGVHVQZCDVIVJZVKUACXGYKEXHXECRXFYJXECGUQURXRYJUSUTUIAFYJVBVCZ
        YKXJVBVCZADGQZFVBVCZYSPVLAXOYJSTZYSYTVMKAYOYGBXTVNZUUCYRAYGBXTOVOZYGUUC
        BCXTXSCRYDYJSXSCGUQVPVRVSFYJVTVHWAWBAXJUUAUCZYIVBAUUBXJUUFVBVCZAUUBYSPW
        CAUUASTZXOUUBUUGVMADXTTZUUDUUHAYLYMYNUUIIJYQCDWDVJZUUEYGUUHBDXTXSDRYDUU
        ASXSDGUQVPVRVSKUUAFVTVHWAADETYIUUFRAXTEDMUUJVKUADXGUUFEXHXEDRXFUUAXEDGU
        QURXRUUAUSUTUIWEWFWHAXKXNHXLAXDXLTZUNZXKXMUCZXJRZXNUULXIUUMXJUULXDETZXI
        UUMRAUUKUUOAXLEXDAXLXTECDWGMWIUOUPZUAXDXGUUMEXHXEXDRXFXMXEXDGUQURXRXMUS
        UTUIWJUULXMUJTZFUJTZUUNXNVMAUUKUUOUUQUUPAEUJGWKZUUOUUQAUJUJWLXQUUSUJWMN
        EUJGWNWOEUJXDGWPWQWRAUURUUKAFKWSWTXMFXAVHXBXCWA $.
        $( [30-Apr-2014] $) $( [22-Jan-2008] $)
    $}

    ${
      ivthle.9 $e |- ( ph -> ( ( F ` A ) <_ U /\ U <_ ( F ` B ) ) ) $.
      $( The intermediate value theorem with weak inequality, increasing case.
         (Contributed by Mario Carneiro, 12-Aug-2014.) $)
      ivthle $p |- ( ph -> E. c e. ( A [,] B ) ( F ` c ) = U ) $=
        ( wceq cr wcel adantr cfv clt wbr wo cv cicc co wrex cle simpld wb wral
        wi ltle syl2anc lbicc2 syl3anc ralrimiva fveq2 eleq1d rcla4v sylc leloe
        mpd mpbid wa simprd ubicc2 cioo wss ioossicc ccncf adantlr simpr ssrexv
        cc mpsyl anassrs eqcom eqeq2d syl5bb rcla4ev sylan jaodan mpdan eqeq1d
        ivth ) ACGUAZFUBUCZWHFQZUDZHUEZGUAZFQZHCDUFUGZUHZAWHFUIUCZWKAWQFDGUAZUI
        UCZPUJAWHRSZFRSZWQWKUKACWOSZBUEZGUAZRSZBWOULZWTACRSZDRSZCDUIUCZXBIJACDU
        BUCZXILAXGXHXJXIUMIJCDUNUOVDZCDUPUQZAXEBWOOURZXEWTBCWOXCCQXDWHRXCCGUSUT
        VAVBKWHFVCUOVEAWIWPWJAWIVFZFWRUBUCZFWRQZUDZWPAXQWIAWSXQAWQWSPVGAXAWRRSZ
        WSXQUKKADWOSZXFXRAXGXHXIXSIJXKCDVHUQZXMXEXRBDWOXCDQXDWRRXCDGUSUTVAVBFWR
        VCUOVETXNXOWPXPAWIXOWPCDVIUGZWOVJAWIXOVFZVFZWNHYAUHWPCDVKYCBCDEFGHAXGYB
        ITAXHYBJTAXAYBKTAXJYBLTAWOEVJYBMTAGEVPVLUGSYBNTAXCWOSXEYBOVMAYBVNWGWNHY
        AWOVOVQVRAXPWPWIAXSXPWPXTWNXPHDWOWNFWMQWLDQZXPWMFVSYDWMWRFWLDGUSVTWAWBW
        CVMWDWEAXBWJWPXLWNWJHCWOWLCQWMWHFWLCGUSWFWBWCWDWE $.
        $( [12-Aug-2014] $)
    $}

    ivthle2.9 $e |- ( ph -> ( ( F ` B ) <_ U /\ U <_ ( F ` A ) ) ) $.
    $( The intermediate value theorem with weak inequality, decreasing case.
       (Contributed by Mario Carneiro, 12-May-2014.) $)
    ivthle2 $p |- ( ph -> E. c e. ( A [,] B ) ( F ` c ) = U ) $=
      ( wceq cr wcel adantr cfv clt wbr wo cv cicc co wrex cle simpld wral ltle
      wb wi syl2anc mpd ubicc2 syl3anc ralrimiva fveq2 eleq1d rcla4v sylc leloe
      mpbid wa simprd lbicc2 cioo wss ioossicc ccncf adantlr simpr ivth2 ssrexv
      cc mpsyl anassrs eqcom eqeq2d syl5bb rcla4ev sylan jaodan mpdan eqeq1d )
      ADGUAZFUBUCZWHFQZUDZHUEZGUAZFQZHCDUFUGZUHZAWHFUIUCZWKAWQFCGUAZUIUCZPUJAWH
      RSZFRSZWQWKUMADWOSZBUEZGUAZRSZBWOUKZWTACRSZDRSZCDUIUCZXBIJACDUBUCZXILAXGX
      HXJXIUNIJCDULUOUPZCDUQURZAXEBWOOUSZXEWTBDWOXCDQXDWHRXCDGUTVAVBVCKWHFVDUOV
      EAWIWPWJAWIVFZFWRUBUCZFWRQZUDZWPAXQWIAWSXQAWQWSPVGAXAWRRSZWSXQUMKACWOSZXF
      XRAXGXHXIXSIJXKCDVHURZXMXEXRBCWOXCCQXDWRRXCCGUTVAVBVCFWRVDUOVETXNXOWPXPAW
      IXOWPCDVIUGZWOVJAWIXOVFZVFZWNHYAUHWPCDVKYCBCDEFGHAXGYBITAXHYBJTAXAYBKTAXJ
      YBLTAWOEVJYBMTAGEVQVLUGSYBNTAXCWOSXEYBOVMAYBVNVOWNHYAWOVPVRVSAXPWPWIAXSXP
      WPXTWNXPHCWOWNFWMQWLCQZXPWMFVTYDWMWRFWLCGUTWAWBWCWDVMWEWFAXBWJWPXLWNWJHDW
      OWLDQWMWHFWLDGUTWGWCWDWEWF $.
      $( [30-Apr-2014] $) $( [22-Jan-2008] $)
  $}

  ${
    $d x z D $.  $d x y z F $.  $d x y z M $.  $d x y z N $.  $d x y z ph $.
    $d x A $.  $d x B $.
    ivthicc.1 $e |- ( ph -> A e. RR ) $.
    ivthicc.2 $e |- ( ph -> B e. RR ) $.
    ivthicc.3 $e |- ( ph -> M e. ( A [,] B ) ) $.
    ivthicc.4 $e |- ( ph -> N e. ( A [,] B ) ) $.
    ivthicc.5 $e |- ( ph -> ( A [,] B ) C_ D ) $.
    ivthicc.7 $e |- ( ph -> F e. ( D -cn-> CC ) ) $.
    ivthicc.8 $e |- ( ( ph /\ x e. ( A [,] B ) ) -> ( F ` x ) e. RR ) $.
    $( The interval between any two points of a continuous real function is
       contained in the range of the function.  Equivalently, the range of a
       continuous real function is convex. $)
    ivthicc $p |- ( ph -> ( ( F ` M ) [,] ( F ` N ) ) C_ ran F ) $=
      ( vz wcel wbr cr ad2antrr vy cfv cicc co crn cv wa clt w3o cle w3a elicc2
      wceq wb syl2anc mpbid simp1d lttri4 adantr wrex wss wral ralrimiva eleq1d
      fveq2 rcla4v sylc iccssre sselda simpr simp2d iccss syl222anc sstrd ccncf
      simp3d cc simpll syldan sylan biimpa 3simpc syl ivthle wi wf ssid sylancr
      wfn cncff ffn fnfvelrn eleq1 syl5ibcom rexlimdva mpd simplr fveq2d oveq2d
      csn cxr rexr eqtr3d eleqtrd elsni sseldd eqeltrd ivthle2 3jaodan mpdan ex
      ccid ssrdv ) AUAGFUBZHFUBZUCUDZFUEZAUAUFZXPQZXRXQQZAXSUGZGHUHRZGHUMZHGUHR
      ZUIZXTAYEXSAGSQZHSQZYEAYFCGUJRZGDUJRZAGCDUCUDZQZYFYHYIUKZKACSQZDSQZYKYLUN
      IJCDGULUOUPZUQZAYGCHUJRZHDUJRZAHYJQZYGYQYRUKZLAYMYNYSYTUNIJCDHULUOUPZUQZG
      HURUOUSYAYBXTYCYDYAYBUGZPUFZFUBZXRUMZPGHUCUDZUTZXTUUCBGHEXRFPAYFXSYBYPTAY
      GXSYBUUBTYAXRSQZYBAXPSXRAXNSQZXOSQZXPSVAAYKBUFZFUBZSQZBYJVBZUUJKAUUNBYJOV
      CZUUNUUJBGYJUULGUMUUMXNSUULGFVEVDVFVGZAYSUUOUUKLUUPUUNUUKBHYJUULHUMUUMXOS
      UULHFVEVDVFVGZXNXOVHUOVIZUSYAYBVJAUUGEVAXSYBAUUGYJEAYMYNYFYGYHYRUUGYJVAIJ
      YPUUBAYFYHYIYOVKAYGYQYRUUAVPCDGHVLVMZMVNZTAFEVQVOUDQZXSYBNTUUCAUULUUGQZUU
      NAXSYBVRAUVCUULYJQZUUNAUUGYJUULUUTVIOVSVTYAXNXRUJRZXRXOUJRZUGZYBYAUUIUVEU
      VFUKZUVGAXSUVHAUUJUUKXSUVHUNUUQUURXNXOXRULUOWAUUIUVEUVFWBWCZUSWDAUUHXTWEX
      SYBAUUFXTPUUGAUUDUUGQUUDEQZUUFXTWEZAUUGEUUDUVAVIAUVJUGUUEXQQZUUFXTAFEWIZU
      VJUVLAEVQFWFZUVMAVQVQVAUVBUVNVQWGNEVQFWJWHEVQFWKWCZEUUDFWLVTUUEXRXQWMWNZV
      SWOTWPYAYCUGZXRXNXQUVQXRXNWTZQXRXNUMUVQXRXPUVRAXSYCWQUVQXNXNUCUDZXPUVRUVQ
      XNXOXNUCUVQGHFYAYCVJWRWSUVQXNXAQZUVSUVRUMAUVTXSYCAUUJUVTUUQXNXBWCTXNXLWCX
      CXDXRXNXEWCAXNXQQZXSYCAUVMGEQUWAUVOAYJEGMKXFEGFWLUOTXGYAYDUGZUUFPHGUCUDZU
      TZXTUWBBHGEXRFPAYGXSYDUUBTAYFXSYDYPTYAUUIYDUUSUSYAYDVJAUWCEVAXSYDAUWCYJEA
      YMYNYGYFYQYIUWCYJVAIJUUBYPAYGYQYRUUAVKAYFYHYIYOVPCDHGVLVMZMVNZTAUVBXSYDNT
      UWBAUULUWCQZUUNAXSYDVRAUWGUVDUUNAUWCYJUULUWEVIOVSVTYAUVGYDUVIUSXHAUWDXTWE
      XSYDAUUFXTPUWCAUUDUWCQUVJUVKAUWCEUUDUWFVIUVPVSWOTWPXIXJXKXM $.
      $( [12-Aug-2014] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y F $.  $d x y ph $.  $d w z ph $.
    $d w z A $.  $d w z B $.  $d w z F $.
    evthicc.1 $e |- ( ph -> A e. RR ) $.
    evthicc.2 $e |- ( ph -> B e. RR ) $.
    evthicc.3 $e |- ( ph -> A <_ B ) $.
    evthicc.4 $e |- ( ph -> F e. ( ( A [,] B ) -cn-> RR ) ) $.
    $( Specialization of the Extreme Value Theorem to a closed interval of
       ` RR ` .  (Contributed by Mario Carneiro, 12-Aug-2014.) $)
    evthicc $p |- ( ph ->
      ( E. x e. ( A [,] B ) A. y e. ( A [,] B ) ( F ` y ) <_ ( F ` x ) /\
        E. z e. ( A [,] B ) A. w e. ( A [,] B ) ( F ` z ) <_ ( F ` w ) ) ) $=
      ( cv cfv co wral wrex eqid cr wcel cle wbr cicc cioo crn csubsp cuni ccmp
      ctg cop icccmp syl2anc ccncf ccn cabs cmin ccom cxp cres copn cc wss wceq
      iccssre ax-resscn a1i sstrd tgioo cncfmet resubmet syl eqtrd eleqtrd ctop
      oveq1d retop uniretop syl6sseq stoig2 sylancr wne lbicc2 syl3anc eqnetrrd
      c0 ne0i evth raleqdv rexeqbidv mpbird evth2 jca ) ACMHNBMHNUAUBZCFGUCOZPZ
      BWNQZDMHNEMHNUAUBZEWNPZDWNQZAWPWMCWNUDUEUINZUJUFNZUGZPZBXBQABCHXAWTXBXBRZ
      WTRZAFSTZGSTZXAUHTIJFGXAWTXEXARUKULZAHWNSUMOZXAWTUNOZLAXIUOUPUQZWNWNURUSZ
      UTNZWTUNOZXJAWNVAVBSVAVBZXIXNVCAWNSVAAXFXGWNSVBZIJFGVDULZXOAVEVFZVGXRWNSX
      LXKSSURUSZXMWTXLRXSRZXMRZXSXSUTNZXTYBRVHVIULAXMXAWTUNAXPXMXAVCXQWNWTXMXEY
      AVJVKVOVLVMZAWNXBWEAWTVNTWNWTUGZVBWNXBVCVPAWNSYDXQVQVRWNWTVSVTZAFWNTZWNWE
      WAAXFXGFGUAUBYFIJKFGWBWCWNFWFVKWDZWGAWOXCBWNXBYEAWMCWNXBYEWHWIWJAWSWQEXBP
      ZDXBQADEHXAWTXBXDXEXHYCYGWKAWRYHDWNXBYEAWQEWNXBYEWHWIWJWL $.
      $( [12-Aug-2014] $)
  $}


