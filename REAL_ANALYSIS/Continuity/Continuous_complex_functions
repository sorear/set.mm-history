$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Continuous complex functions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the new symbol needed for continuous complex functions. $)
  $c -cn-> $.

  $( Extend class notation to include the operation which returns a class of
     continuous complex functions. $)
  ccncf $a class -cn-> $.

  ${
    $d a b d e f x y $.
    $( Define the operation whose value is a class of continuous complex
       functions. $)
    df-cncf $a |- -cn-> = ( a e. ~P CC , b e. ~P CC |->
      { f e. ( b ^m a ) | A. x e. a A. e e. RR+ E. d e. RR+ A. y e. a
  ( ( abs ` ( x - y ) ) < d -> ( abs ` ( ( f ` x ) - ( f ` y ) ) ) < e ) } ) $.
  $}

  ${
    $d a b f w x y z A $.  $d f w x y z F $.  $d w x y z P $.  $d w x y z Q $.
    $d b f w x y z B $.  $d w x y z C $.  $d w y z R $.
    $( The value of the continuous complex function operation is the set of
       continuous functions from ` A ` to ` B ` .  (Contributed by Paul
       Chapman, 11-Oct-2007.) $)
    cncfval $p |- ( ( A C_ CC /\ B C_ CC ) ->
      ( A -cn-> B ) = { f e. ( B ^m A ) | A. x e. A A. y e. RR+
        E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
        ( abs ` ( ( f ` x ) - ( f ` w ) ) ) < y ) } ) $=
      ( va vb cc wss wcel co cv cfv wral crp cmap crab wceq cpw ccncf cmin cabs
      clt wbr wi wrex cnex elpw2 oveq2 raleq rexbidv raleqbi1dv rabeqbidv oveq1
      ralbidv rabeq syl df-cncf ovex rabex ovmpt2 syl2anbr ) EJKEJUAZLFVELEFUBM
      ANZDNZUCMUDOCNUEUFVFGNZOVGVHOUCMUDOBNUEUFUGZDEPZCQUHZBQPZAEPZGFERMZSZTFJK
      EJUIUJFJUIUJHIEFVEVEVIDHNZPZCQUHZBQPZAVPPZGINZVPRMZSVOUBVMGWAERMZSZVPETZV
      TVMGWBWCVPEWARUKVSVLAVPEWEVRVKBQWEVQVJCQVIDVPEULUMUQUNUOWAFTWCVNTWDVOTWAF
      ERUPVMGWCVNURUSADBGHICUTVMGVNFERVAVBVCVD $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)

    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 11-Oct-2007.) $)
    elcncf $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( x - w ) ) < z ->
     ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) ) $=
      ( vf cc wa co wcel cv cmin cabs cfv clt wral crp cvv wss cmap wbr wi wrex
      ccncf wf crab cncfval eleq2d wceq fveq1 oveq12d fveq2d breq12d rexralbidv
      eqidd imbi2d 2ralbidv elrab syl6bb cnex ssex elmapg syl2anr anbi1d bitrd
      wb ) EIUAZFIUAZJZGEFUFKZLZGFEUBKZLZAMZDMZNKOPCMQUCZVPGPZVQGPZNKZOPZBMZQUC
      ZUDZDERCSUEZBSRAERZJZEFGUGZWGJVKVMGVRVPHMZPZVQWJPZNKZOPZWCQUCZUDZDERCSUEZ
      BSRAERZHVNUHZLWHVKVLWSGABCDEFHUIUJWRWGHGVNWJGUKZWQWFABESWTWPWECDSEWTWOWDV
      RWTWNWBWCWCQWTWMWAOWTWKVSWLVTNVPWJGULVQWJGULUMUNWTWCUQUOURUPUSUTVAVKVOWIW
      GVJFTLETLVOWIVHVIFIVBVCEIVBVCFEGTTVDVEVFVG $.
      $( [9-Nov-2013] $) $( [11-Oct-2007] $)

    $( Version of ~ elcncf with arguments commuted.  (Contributed by Mario
       Carneiro, 28-Apr-2014.) $)
    elcncf2 $p |- ( ( A C_ CC /\ B C_ CC ) ->
     ( F e. ( A -cn-> B ) <-> ( F : A --> B /\ A. x e. A A. y e. RR+
     E. z e. RR+ A. w e. A ( ( abs ` ( w - x ) ) < z ->
     ( abs ` ( ( F ` w ) - ( F ` x ) ) ) < y ) ) ) ) $=
      ( cc wa co wcel cv cmin cabs cfv clt wbr wral crp sseldd ccncf wf wi wrex
      elcncf wb wceq simplll simprl simprr abssub syl2anc breq1d simpllr simplr
      wss ffvelrn imbi12d anassrs ralbidva rexbidv ralbidv pm5.32da bitrd ) EHU
      PZFHUPZIZGEFUAJKEFGUBZALZDLZMJNOZCLZPQZVIGOZVJGOZMJNOZBLZPQZUCZDERZCSUDZB
      SRZAERZIVHVJVIMJNOZVLPQZVOVNMJNOZVQPQZUCZDERZCSUDZBSRZAERZIABCDEFGUEVGVHW
      CWLVGVHIZWBWKAEWMVIEKZIZWAWJBSWOVTWICSWOVSWHDEWMWNVJEKZVSWHUFWMWNWPIZIZVM
      WEVRWGWRVKWDVLPWRVIHKVJHKVKWDUGWREHVIVEVFVHWQUHZWMWNWPUIZTWREHVJWSWMWNWPU
      JZTVIVJUKULUMWRVPWFVQPWRVNHKVOHKVPWFUGWRFHVNVEVFVHWQUNZWRVHWNVNFKVGVHWQUO
      ZWTEFVIGUQULTWRFHVOXBWRVHWPVOFKXCXAEFVJGUQULTVNVOUKULUMURUSUTVAVBUTVCVD
      $.
      $( [28-Apr-2014] $)

    $( Reverse closure of the continuous function predicate.  (Contributed by
       Mario Carneiro, 25-Aug-2014.) $)
    cncfrss $p |- ( F e. ( A -cn-> B ) -> A C_ CC ) $=
      ( vx vw vz vf vy va vb ccncf co wcel cc c0 cfv cv cmin cabs wral cop ne0i
      cpw cxp wss cdm wne wn df-ov ndmfv syl5eq necon1ai syl wfn clt wbr wi crp
      wceq wrex cmap crab cvv ovex rabex rgen2w fnmpt2 mpbi fndm ax-mp syl6eleq
      df-cncf opelxp1 elpwi 3syl ) CABKLZMZABUAZNUCZVSUDZMAVSMANUEVQVRKUFZVTVQV
      POUGVRWAMZVPCUBWBVPOWBUHVPVRKPOABKUIVRKUJUKULUMKVTUNZWAVTUSDQZEQZRLSPFQUO
      UPWDGQZPWEWFPRLSPHQUOUPUQEIQZTFURUTHURTDWGTZGJQZWGVALZVBZVCMZJVSTIVSTWCWL
      IJVSVSWHGWJWIWGVAVDVEVFIJVSVSWKKDEHGIJFVLVGVHVTKVIVJVKABVSVSVMANVNVO $.
      $( [25-Aug-2014] $)

    $( Reverse closure of the continuous function predicate.  (Contributed by
       Mario Carneiro, 25-Aug-2014.) $)
    cncfrss2 $p |- ( ( F e. ( A -cn-> B ) /\ B e. V ) -> B C_ CC ) $=
      ( vx vw vz vf vy va vb wcel ccncf co cc c0 cfv cv cmin wral wss cop df-ov
      cpw cxp cdm wne ne0i wn ndmfv syl5eq necon1ai syl wfn wceq clt wbr wi crp
      cabs wrex cmap crab cvv ovex rabex rgen2w fnmpt2 mpbi fndm ax-mp syl6eleq
      df-cncf wa opelxpg elpwi adantl syl6bi syl5 impcom ) BDLZCABMNZLZBOUAZWCA
      BUBZOUDZWFUEZLZWAWDWCWEMUFZWGWCWBPUGWEWILZWBCUHWJWBPWJUIWBWEMQPABMUCWEMUJ
      UKULUMMWGUNZWIWGUOERZFRZSNUTQGRUPUQWLHRZQWMWNQSNUTQIRUPUQURFJRZTGUSVAIUST
      EWOTZHKRZWOVBNZVCZVDLZKWFTJWFTWKWTJKWFWFWPHWRWQWOVBVEVFVGJKWFWFWSMEFIHJKG
      VMVHVIWGMVJVKVLWAWHAWFLZBWFLZVNWDABWFWFDVOXBWDXABOVPVQVRVSVT $.
      $( [25-Aug-2014] $)

    $( A continuous complex function's domain and codomain.  (Contributed by
       Paul Chapman, 17-Jan-2008.)  (Revised by Mario Carneiro,
       25-Aug-2014.) $)
    cncff $p |- ( ( B C_ CC /\ F e. ( A -cn-> B ) ) ->
                  F : A --> B ) $=
      ( vx vw vz vy cc wss ccncf co wa cv cmin cabs cfv clt wbr wral crp elcncf
      wcel wf wi wrex simpr wb cncfrss id syl2anr mpbid simpld ) BHIZCABJKUBZLZ
      ABCUCZDMZEMZNKOPFMQRUQCPURCPNKOPGMQRUDEASFTUEGTSDASZUOUNUPUSLZUMUNUFUNAHI
      UMUNUTUGUMABCUHUMUIDGFEABCUAUJUKUL $.
      $( [25-Aug-2014] $) $( [17-Jan-2008] $)

    $( Defining property of a continuous function.  (Contributed by Mario
       Carneiro, 30-Apr-2014.) $)
    cncfi $p |- ( ( ( B C_ CC /\ F e. ( A -cn-> B ) ) /\
                    ( C e. A /\ R e. RR+ ) ) ->
                  E. z e. RR+ A. w e. A ( ( abs ` ( w - C ) ) < z ->
                    ( abs ` ( ( F ` w ) - ( F ` C ) ) ) < R ) ) $=
      ( vx vy co wcel wa cv cmin cabs cfv clt wbr wral crp cc wss ccncf wi wrex
      wf simpr wb cncfrss elcncf2 syl2anr mpbid simprd wceq oveq2 fveq2d breq1d
      id fveq2 oveq2d imbi12d rexralbidv breq2 imbi2d rcla42v mpan9 ) DUAUBZGCD
      UCJKZLZBMZHMZNJZOPZAMZQRZVJGPZVKGPZNJZOPZIMZQRZUDZBCSATUEZITSHCSZECKFTKLV
      JENJZOPZVNQRZVPEGPZNJZOPZFQRZUDZBCSATUEZVICDGUFZWDVIVHWNWDLZVGVHUGVHCUAUB
      VGVHWOUHVGCDGUIVGURHIABCDGUJUKULUMWCWMWGWJVTQRZUDZBCSATUEHIEFCTVKEUNZWBWQ
      ABTCWRVOWGWAWPWRVMWFVNQWRVLWEOVKEVJNUOUPUQWRVSWJVTQWRVRWIOWRVQWHVPNVKEGUS
      UTUPUQVAVBVTFUNZWQWLABTCWSWPWKWGVTFWJQVCVDVBVEVF $.
      $( [25-Aug-2014] $) $( [30-Apr-2014] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z F $.  $d w x y ph $.
    $d w z Z $.
    elcncf1d.1 $e |- ( ph -> F : A --> B ) $.
    elcncf1d.2 $e |- ( ph -> ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) ) $.
    elcncf1d.3 $e |- ( ph -> ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1di $p |- ( ph ->
                   ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) ) $=
      ( vz co wcel wa cv cfv clt wral crp ccncf cc wss wf cmin cabs wbr wi wrex
      imp an32 anbi2i anass bitr4i sylbir ralrimiva wceq imbi1d ralbidv rcla4ev
      breq2 syl2anc anassrs jca elcncf syl5ibrcom ) AGEFUAMNEUBUCFUBUCOEFGUDZBP
      ZDPZUEMUFQZLPZRUGZVHGQVIGQUEMUFQCPZRUGZUHZDESZLTUIZCTSZBESZOAVGVSIAVRBEAV
      HENZOVQCTAVTVMTNZVQAVTWAOZOZHTNZVJHRUGZVNUHZDESZVQAWBWDJUJWCWFDEWCVIENZOZ
      AVTWHOWAOZOZWFWKAWBWHOZOWIWJWLAVTWHWAUKULAWBWHUMUNAWJWFKUJUOUPVPWGLHTVKHU
      QZVOWFDEWMVLWEVNVKHVJRVAURUSUTVBVCUPUPVDBCLDEFGVEVF $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d w x y A $.  $d w x y B $.  $d w x y F $.  $d w Z $.
    elcncf1i.1 $e |- F : A --> B $.
    elcncf1i.2 $e |- ( ( x e. A /\ y e. RR+ ) -> Z e. RR+ ) $.
    elcncf1i.3 $e |- ( ( ( x e. A /\ w e. A ) /\ y e. RR+ ) ->
                       ( ( abs ` ( x - w ) ) < Z ->
                         ( abs ` ( ( F ` x ) - ( F ` w ) ) ) < y ) ) $.
    $( Membership in the set of continuous complex functions from ` A ` to
       ` B ` .  (Contributed by Paul Chapman, 26-Nov-2007.) $)
    elcncf1ii $p |- ( ( A C_ CC /\ B C_ CC ) -> F e. ( A -cn-> B ) ) $=
      ( cc wss wa co wcel wi wtru a1i cv cfv wf crp cmin cabs clt wbr elcncf1di
      ccncf trud ) DKLEKLMFDEUHNOPQABCDEFGDEFUAQHRASZDOZBSZUBOZMGUBOPQIRUKCSZDO
      MUMMUJUNUCNUDTGUEUFUJFTUNFTUCNUDTULUEUFPPQJRUGUI $.
      $( [26-Nov-2007] $)
  $}

  ${
    $d f w x y z A $.  $d f w x y z B $.  $d f w x y z C $.  $d w x y z F $.
    $( A continuous complex function restricted to a subset is continuous.
       (Contributed by Paul Chapman, 18-Oct-2007.)  (Revised by Mario Carneiro,
       25-Aug-2014.) $)
    rescncf $p |- ( ( B C_ CC /\ C C_ A ) ->
            ( F e. ( A -cn-> B ) -> ( F |` C ) e. ( C -cn-> B ) ) ) $=
      ( vx vw vz vy cc wss co wcel cv cmin cabs cfv clt wral crp wa cres w3a wf
      ccncf wbr wi wrex simp2 cncfrss 3ad2ant3 sstrd simp1 elcncf syl2anc simp3
      wb mpbid simpld fssres simprd ssralv fvres oveqan12d fveq2d breq1d imbi2d
      biimprd ralimdva sylan9 reximdv ralimdv syld sylc mpbir2and 3expia ) BIJZ
      CAJZDABUDKLZDCUAZCBUDKLZVPVQVRUBZVTCBVSUCZEMZFMZNKOPGMQUEZWCVSPZWDVSPZNKZ
      OPZHMZQUEZUFZFCRZGSUGZHSRZECRZWACIJVPVTWBWPTUPWACAIVPVQVRUHZVRVPAIJZVQABD
      UIUJZUKVPVQVRULZEHGFCBVSUMUNWAABDUCZVQWBWAXAWEWCDPZWDDPZNKZOPZWJQUEZUFZFA
      RZGSUGZHSRZEARZWAVRXAXKTZVPVQVRUOWAWRVPVRXLUPWSWTEHGFABDUMUNUQZURWQABCDUS
      UNWAVQXKWPWQWAXAXKXMUTVQXKXJECRWPXJECAVAVQXJWOECVQWCCLZTZXIWNHSXOXHWMGSVQ
      XHXGFCRXNWMXGFCAVAXNXGWLFCXNWDCLZTZWLXGXQWKXFWEXQWIXEWJQXQWHXDOXNXPWFXBWG
      XCNWCCDVBWDCDVBVCVDVEVFVGVHVIVJVKVHVLVMVNVO $.
      $( [25-Aug-2014] $) $( [18-Oct-2007] $)

    $( Change the codomain of a continuous complex function.  (Contributed by
       Paul Chapman, 18-Oct-2007.)  (Revised by Mario Carneiro,
       25-Aug-2014.) $)
    cncffvrn $p |- ( ( B C_ CC /\ C C_ CC /\ A. x e. A ( F ` x ) e. C ) ->
      ( F e. ( A -cn-> B ) -> F e. ( A -cn-> C ) ) ) $=
      ( vw vz vy cc wss cv cfv wcel wral ccncf co wa wf cmin syl2anc w3a clt wi
      cabs wbr crp wrex cncfrss adantl simpl2 elcncf simpr simpl1 simpld simpl3
      mpbid crn wfn ffn fnfvrnss sylan dffn3 sylib fss syldan simprd mpbir2and
      wb ex ) CIJZDIJZAKZELZDMABNZUAZEBCOPMZEBDOPMZVOVPQZVQBDERZVLFKZSPUDLGKUBU
      EVMVTELSPUDLHKUBUEUCFBNGUFUGHUFNABNZVRBIJZVKVQVSWAQVHVPWBVOBCEUHUIZVJVKVN
      VPUJAHGFBDEUKTVRBCERZVNVSVRWDWAVRVPWDWAQZVOVPULVRWBVJVPWEVHWCVJVKVNVPUMAH
      GFBCEUKTUPZUNVJVKVNVPUOWDVNEUQZDJZVSWDEBURZVNWHBCEUSZABDEUTVAWDBWGERZWHVS
      WDWIWKWJBEVBVCBWGDEVDVAVETVRWDWAWFVFVGVI $.
      $( [25-Aug-2014] $) $( [18-Oct-2007] $)

    $( The set of continuous functions is expanded when the range is expanded.
       (Contributed by Mario Carneiro, 30-Aug-2014.) $)
    cncfss $p |- ( ( B C_ C /\ C C_ CC ) -> ( A -cn-> B ) C_ ( A -cn-> C ) ) $=
      ( vf vx wss cc wa ccncf co cv wcel simpr cfv wral wi sstr adantr simplr
      wf cncff simpll fss syl2anc ffvelrn ralrimiva syl cncffvrn syl3anc mpd ex
      sylan ssrdv ) BCFZCGFZHZDABIJZACIJZUPDKZUQLZUSURLZUPUTHZUTVAUPUTMVBBGFZUO
      EKZUSNCLZEAOZUTVAPUPVCUTBCGQZRUNUOUTSVBACUSTZVFVBABUSTZUNVHUPVCUTVIVGABUS
      UAULUNUOUTUBABCUSUCUDVHVEEAACVDUSUEUFUGEABCUSUHUIUJUKUM $.
      $( [30-Aug-2014] $)
  $}

  ${
    $d w x y z $.
    $( Absolute value is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    abscncf $p |- abs e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cabs cc cr ccncf co wcel wf cv cmin cfv clt wbr wi wral crp
      wss wrex wa wb ssid ax-resscn elcncf2 mp2an absf abscn2 rgen2 mpbir2an )
      EFGHIJZFGEKZALZBLZMIENCLOPUNENUOENMIENDLOPQAFRCSUAZDSRBFRZFFTGFTULUMUQUBU
      CFUDUEBDCAFGEUFUGUHUPBDFSDCAUOUIUJUK $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Real part is continuous.  (Contributed by Paul Chapman, 21-Oct-2007.) $)
    recncf $p |- Re e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cre cc cr ccncf co wcel wf cv cmin cabs cfv clt wbr crp wss
      wral wi wrex wa wb ssid ax-resscn elcncf2 mp2an ref recn2 rgen2 mpbir2an
      ) EFGHIJZFGEKZALZBLZMINOCLPQUOEOUPEOMINODLPQUAAFTCRUBZDRTBFTZFFSGFSUMUNUR
      UCUDFUEUFBDCAFGEUGUHUIUQBDFRDCAUPUJUKUL $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Imaginary part is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    imcncf $p |- Im e. ( CC -cn-> RR ) $=
      ( vw vx vz vy cim cc cr ccncf co wcel wf cv cmin cabs cfv clt wbr crp wss
      wral wi wrex wa wb ssid ax-resscn elcncf2 mp2an imf imcn2 rgen2 mpbir2an
      ) EFGHIJZFGEKZALZBLZMINOCLPQUOEOUPEOMINODLPQUAAFTCRUBZDRTBFTZFFSGFSUMUNUR
      UCUDFUEUFBDCAFGEUGUHUIUQBDFRDCAUPUJUKUL $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)

    $( Complex conjugate is continuous.  (Contributed by Paul Chapman,
       21-Oct-2007.) $)
    cjcncf $p |- * e. ( CC -cn-> CC ) $=
      ( vw vx vz vy ccj cc ccncf co wcel wf cmin cabs cfv clt wbr wral crp wrex
      cv wi wss wa wb ssid elcncf2 mp2an cjf cjcn2 rgen2 mpbir2an ) EFFGHIZFFEJ
      ZASZBSZKHLMCSNOUMEMUNEMKHLMDSNOTAFPCQRZDQPBFPZFFUAZUQUKULUPUBUCFUDZURBDCA
      FFEUEUFUGUOBDFQDCAUNUHUIUJ $.
      $( [28-Apr-2014] $) $( [21-Oct-2007] $)
  $}

  ${
    $d t u v w x y z A $.  $d t u w y z F $.
    mulc1cncf.1 $e |- F = ( x e. CC |-> ( A x. x ) ) $.
    $( Multiplication by a constant is continuous.  (Contributed by Paul
       Chapman, 28-Nov-2007.)  (Revised by Mario Carneiro, 30-Apr-2014.) $)
    mulc1cncf $p |- ( A e. CC -> F e. ( CC -cn-> CC ) ) $=
      ( vu vw cc wcel cv cmin co cabs cfv clt wbr wral crp cmul wa cc0 vy vz vv
      vt wf wi wrex ccncf mulcl fmptd simprr simpl simprl mulcn2 syl3anc fveq2d
      wceq oveq1 breq1d anbi1d oveq1d imbi12d ralbidv rcla4v ad2antrr wb syl6eq
      subid abs0 simprll rpgt0 syl eqbrtrd biantrurd oveq2 ovex simplrl oveq12d
      fvmpt anassrs ralbidva sylibrd reximdva rexlimdva mpd ralrimivva wss ssid
      elcncf2 mp2an sylanbrc ) BGHZGGCUEZEIZUAIZJKLMFIZNOZWNCMZWOCMZJKZLMZUBIZN
      OZUFZEGPZFQUGZUBQPUAGPZCGGUHKHZWLAGBAIZRKZGCBXIUIDUJWLXFUAUBGQWLWOGHZXBQH
      ZSZSZUCIZBJKZLMZUDIZNOZWQSZXOWNRKZBWORKZJKZLMZXBNOZUFZEGPZUCGPZFQUGZUDQUG
      ZXFXNXLWLXKYJWLXKXLUKWLXMULWLXKXLUMUDFEUCXBBWOUNUOXNYIXFUDQXNXRQHZSYHXEFQ
      XNYKWPQHZYHXEUFXNYKYLSZSZYHBBJKZLMZXRNOZWQSZBWNRKZYBJKZLMZXBNOZUFZEGPZXEW
      LYHUUDUFXMYMYGUUDUCBGXOBUQZYFUUCEGUUEXTYRYEUUBUUEXSYQWQUUEXQYPXRNUUEXPYOL
      XOBBJURUPUSUTUUEYDUUAXBNUUEYCYTLUUEYAYSYBJXOBWNRURVAUPUSVBVCVDVEYNXDUUCEG
      XNYMWNGHZXDUUCVFXNYMUUFSZSZWQYRXCUUBUUHYQWQUUHYPTXRNUUHYPTLMTUUHYOTLWLYOT
      UQXMUUGBVHVEUPVIVGUUHYKTXRNOXNYKYLUUFVJXRVKVLVMVNUUHXAUUAXBNUUHWTYTLUUHWR
      YSWSYBJUUHUUFWRYSUQXNYMUUFUKAWNXJYSGCXIWNBRVODBWNRVPVSVLUUHXKWSYBUQWLXKXL
      UUGVQAWOXJYBGCXIWOBRVODBWORVPVSVLVRUPUSVBVTWAWBVTWCWDWEWFGGWGZUUIXHWMXGSV
      FGWHZUUJUAUBFEGGCWIWJWK $.
      $( [30-Apr-2014] $) $( [28-Nov-2007] $)
  $}

  ${
    $d A x $.
    divccncf.1 $e |- F = ( x e. CC |-> ( x / A ) ) $.
    $( Division by a constant is continuous.  (Contributed by Paul Chapman,
       28-Nov-2007.) $)
    divccncf $p |- ( ( A e. CC /\ A =/= 0 ) -> F e. ( CC -cn-> CC ) ) $=
      ( cc wcel cc0 wne wa c1 cdiv co cmul cmpt ccncf wceq divrec2 3expb ancoms
      cv mpteq2dva syl5eq reccl eqid mulc1cncf syl eqeltrd ) BEFZBGHZIZCAEJBKLZ
      ATZMLZNZEEOLZUJCAEULBKLZNUNDUJAEUPUMULEFZUJUPUMPZUQUHUIURULBQRSUAUBUJUKEF
      UNUOFBUCAUKUNUNUDUEUFUG $.
      $( [28-Nov-2007] $)
  $}

  ${
    $d u w x y z A $.  $d u v w z B $.  $d u v w x y z C $.  $d u w x y z ph $.
    $d u v w x y z F $.  $d u v w x y z G $.
    cncfco.3 $e |- ( ph -> C C_ CC ) $.
    cncfco.4 $e |- ( ph -> F e. ( A -cn-> B ) ) $.
    cncfco.5 $e |- ( ph -> G e. ( B -cn-> C ) ) $.
    $( The composition of two continuous maps on complex numbers is also
       continuous.  (Contributed by Jeff Madsen, 2-Sep-2009.)  (Revised by
       Mario Carneiro, 30-Apr-2014.) $)
    cncfco $p |- ( ph -> ( G o. F ) e. ( A -cn-> C ) ) $=
      ( vw vz co wcel cmin cabs cfv clt wi crp wa vx vy vv vu ccom ccncf wf wbr
      cv wral cc wss wb cncfrss syl elcncf2 syl2anc cncff adantr simprl ffvelrn
      wrex fco simprr cncfi syl22anc ad2antrr simpr ad3antrrr wceq oveq1 fveq2d
      simplrl breq1d fveq2 oveq1d imbi12d rcla4v wfun ffun fvco3 syl3anc imbi2d
      oveq12d sylibrd imp an32s imim2d anassrs ralimdva reximdva mpid rexlimdva
      ex mpd ralrimivva mpbir2and ) AFEUEZBDUFLMZBDWRUGZJUIZUAUIZNLOPKUIZQUHZXA
      WRPZXBWRPZNLZOPZUBUIZQUHZRZJBUJZKSVBZUBSUJUABUJZABUKULZDUKULZWSWTXNTUMAEB
      CUFLMZXOHBCEUNUOGUAUBKJBDWRUPUQACDFUGZBCEUGZWTAXPFCDUFLMZXRGICDFURUQZACUK
      ULZXQXSAXTYBICDFUNUOZHBCEURUQZBCDFEVCUQAXMUAUBBSAXBBMZXISMZTZTZUCUIZXBEPZ
      NLZOPZUDUIZQUHZYIFPZYJFPZNLZOPZXIQUHZRZUCCUJZUDSVBZXMYHXPXTYJCMZYFUUBAXPY
      GGUSAXTYGIUSYHXSYEUUCAXSYGYDUSAYEYFUTBCXBEVAUQAYEYFVDUDUCCDYJXIFVEVFYHUUA
      XMUDSYHYMSMZTZUUAXDXAEPZYJNLZOPZYMQUHZRZJBUJZKSVBZXMUUEYBXQYEUUDUULAYBYGU
      UDYCVGAXQYGUUDHVGAYEYFUUDVMZYHUUDVHKJBCXBYMEVEVFUUEUUAUULXMRUUEUUATZUUKXL
      KSUUNXCSMZTUUJXKJBUUNUUOXABMZUUJXKRUUNUUOUUPTZTUUIXJXDUUEUUQUUAUUIXJRZUUE
      UUQTZUUAUURUUSUUAUUIUUFFPZYPNLZOPZXIQUHZRZUURUUSUUFCMZUUAUVDRUUSXSUUPUVEA
      XSYGUUDUUQYDVIZUUEUUOUUPVDZBCXAEVAUQYTUVDUCUUFCYIUUFVJZYNUUIYSUVCUVHYLUUH
      YMQUVHYKUUGOYIUUFYJNVKVLVNUVHYRUVBXIQUVHYQUVAOUVHYOUUTYPNYIUUFFVOVPVLVNVQ
      VRUOUUSXJUVCUUIUUSXHUVBXIQUUSXGUVAOUUSXEUUTXFYPNUUSFVSZXSUUPXEUUTVJAUVIYG
      UUDUUQAXRUVIYACDFVTUOVIZUVFUVGBCXAFEWAWBUUSUVIXSYEXFYPVJUVJUVFUUEYEUUQUUM
      USBCXBFEWAWBWDVLVNWCWEWFWGWHWIWJWKWNWLWMWOWPWQ $.
      $( [25-Aug-2014] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y F $.  $d x A $.
    negfcncf.1 $e |- G = ( x e. A |-> -u ( F ` x ) ) $.
    $( The negative of a continuous complex function is continuous.
       (Contributed by Paul Chapman, 21-Jan-2008.) $)
    negfcncf $p |- ( F e. ( A -cn-> CC ) -> G e. ( A -cn-> CC ) ) $=
      ( vy cc ccncf co wcel c1 cneg cv cmul cmpt ccom cfv wf wceq a1i wss cncff
      ssid mpan ffvelrn sylan wfn ffn dffn5v sylib mulm1 mpteq2ia negeq syl6eqr
      syl fmptco id ax-1cn negcli eqid mulc1cncf ax-mp cncfco eqeltrrd ) CBGHIZ
      JZFGKLZFMZNIZOZCPZDVEVFVKABAMZCQZLZODVFAFBGVMVHLZVNCVJVFBGCRZVLBJVMGJGGUA
      ZVFVPGUCZBGCUBUDZBGVLCUEUFVFCBUGZCABVMOSVFVPVTVSBGCUHUOABCUIUJVJFGVOOSVFF
      GVIVOVHUKULTVHVMUMUPEUNVFBGGCVJVQVFVRTVFUQVJGGHIJZVFVGGJWAKURUSFVGVJVJUTV
      AVBTVCVD $.
      $( [25-Aug-2014] $) $( [21-Jan-2008] $)
  $}

  ${
    $d A f w x y z $.  $d B f w x y z $.  $d C w x y z $.  $d D w x y z $.
    $d J f w x y z $.  $d K f w x y $.
    cncfmet.1 $e |- C = ( ( abs o. - ) |` ( A X. A ) ) $.
    cncfmet.2 $e |- D = ( ( abs o. - ) |` ( B X. B ) ) $.
    cncfmet.3 $e |- J = ( MetOpen ` C ) $.
    cncfmet.4 $e |- K = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 26-Nov-2007.) $)
    cncfmet $p |- ( ( A C_ CC /\ B C_ CC ) ->
                       ( A -cn-> B ) = ( J Cn K ) ) $=
      ( vx vw cc wa co wf wral wcel wceq cr vf vz vy wss ccncf ccn cv cmin cabs
      cfv clt wbr wi crp wrex cdm wb cxp ccom cres absf subopr fco mp2an xpss12
      anidms fssres sylancr feq1i sylibr fdm dmeqd dmxpid syl6eq feq23 ad2antrr
      syl syl2an anbi1d raleqdv adantr simplll simprl simprr oveqi ovres syl5eq
      ad2ant2l ssel2 eqid cnmetdval eqtrd breq1d simplr ffvelrn syl2anc simpllr
      syl22anc sseldd imbi12d anassrs ralbidva bitrd rexbidv ralbidv bitr2d cme
      pm5.32da elcncf cnmet metres ax-mp eqeltri metcn a1i 3bitr4d eqrdv ) AMUD
      ZBMUDZNZUAABUEOZEFUFOZXTABUAUGZPZKUGZLUGZUHOUIUJZUBUGZUKULZYEYCUJZYFYCUJZ
      UHOUIUJZUCUGZUKULZUMZLAQZUBUNUOZUCUNQZKAQZNZCUPZUPZDUPZUPZYCPZYEYFCOZYHUK
      ULZYJYKDOZYMUKULZUMZLUUBQZUBUNUOZUCUNQZKUUBQZNZYCYARYCYBRZXTUUOYDUUNNYTXT
      UUEYDUUNXRUUBASZUUDBSZUUEYDUQXSXRAAURZTCPZUUQXRUUSTUIUHUSZUUSUTZPZUUTXRMM
      URZTUVAPZUUSUVDUDZUVCMTUIPUVDMUHPUVEVAVBUVDMTUIUHVCVDZXRUVFAMAMVEVFUVDTUU
      SUVAVGVHUUSTCUVBGVIVJUUTUUBUUSUPAUUTUUAUUSUUSTCVKVLAVMVNVQZXSBBURZTDPZUUR
      XSUVITUVAUVIUTZPZUVJXSUVEUVIUVDUDZUVLUVGXSUVMBMBMVEVFUVDTUVIUVAVGVHUVITDU
      VKHVIVJUVJUUDUVIUPBUVJUUCUVIUVITDVKVLBVMVNVQUUBUUDABYCVOVRVSXTYDUUNYSXTYD
      NZUUNUUMKAQYSUVNUUMKUUBAXRUUQXSYDUVHVPZVTUVNUUMYRKAUVNYEARZNZUULYQUCUNUVQ
      UUKYPUBUNUVQUUKUUJLAQYPUVQUUJLUUBAUVNUUQUVPUVOWAVTUVQUUJYOLAUVNUVPYFARZUU
      JYOUQUVNUVPUVRNZNZUUGYIUUIYNUVTUUFYGYHUKUVTXRUVPXRUVRUUFYGSXRXSYDUVSWBZUV
      NUVPUVRWCZUWAUVNUVPUVRWDZXRUVPNZXRUVRNZNUUFYEYFUVAOZYGUVPUVRUUFUWFSXRXRUV
      SUUFYEYFUVBOUWFCUVBYEYFGWEYEYFAAUVAWFWGWHUWDYEMRYFMRUWFYGSUWEAMYEWIAMYFWI
      YEYFUVAUVAWJZWKVRWLWRWMUVTUUHYLYMUKUVTUUHYJYKUVAOZYLUVTYJBRZYKBRZUUHUWHSU
      VTYDUVPUWIXTYDUVSWNZUWBABYEYCWOWPZUVTYDUVRUWJUWKUWCABYFYCWOWPZUWIUWJNUUHY
      JYKUVKOUWHDUVKYJYKHWEYJYKBBUVAWFWGWPUVTYJMRYKMRUWHYLSUVTBMYJXRXSYDUVSWQZU
      WLWSUVTBMYKUWNUWMWSYJYKUVAUWGWKWPWLWMWTXAXBXCXDXEXBXCXHXFKUCUBLABYCXIUUPU
      UOUQZXTCXGRDXGRUWOCUVBXGGUVAXGRZUVBXGRUVAUWGXJZUVAAXKXLXMDUVKXGHUWPUVKXGR
      UWQUVABXKXLXMKUCUBLCDYCEFUUBUUDUUBWJIUUDWJJXNVDXOXPXQ $.
      $( [13-Nov-2013] $) $( [11-Oct-2007] $)
  $}

  ${
    cncfmet1.1 $e |- D = ( abs o. - ) $.
    cncfmet1.2 $e |- J = ( MetOpen ` D ) $.
    $( Relate complex function continuity to metric space continuity.
       (Contributed by Paul Chapman, 28-Nov-2007.) $)
    cncfmet1 $p |- ( CC -cn-> CC ) = ( J Cn J ) $=
      ( cc wss ccncf co ccn wceq ssid cabs cmin ccom cxp cdm subopr ax-mp mp2an
      fdmi cres wrel relco crn wf absf sseqtr4i dmcosseq eqtri eqimssi relssres
      frn cr eqtr4i cncfmet ) EEFZUPEEGHBBIHJEKZUQEEAABBALMNZUREEOZUAZCURUBURPZ
      USFUTURJLMUCVAUSVAMPZUSMUDZLPZFVAVBJVCEVDUSEMUEVCEFQUSEMULREUMLUFTUGLMUHR
      USEMQTUIUJURUSUKSUNZVEDDUOS $.
      $( [28-Nov-2007] $)
  $}

  $( Absolute value is continuous.  Alternate proof of ~ abscncf . $)
  abscncfALT $p |- abs e. ( CC -cn-> RR ) $=
    ( cabs cmin ccom copn cfv cr cxp cres ccn co ccncf eqid abscn wss wceq ssid
    cc ax-resscn cdm mp2an wrel relco dmcoss subopr fdmi sseqtri eqcomi cncfmet
    relssres eleqtrri ) AABCZDEZUKFFGHZDEZIJZQFKJZUKUMULUNUKLUMLZULLZUNLZMQQNFQ
    NUPUOOQPRQFUKUMULUNUKQQGZHZUKUKUAUKSZUTNVAUKOABUBVBBSUTABUCUTQBUDUEUFUKUTUI
    TUGUQURUSUHTUJ $.
    $( [6-Jun-2008] $)

  ${
    $d z F $.  $d z J $.  $d x y z K $.
    cnrehmeo.1 $e |- F = ( x e. RR , y e. RR |-> ( x + ( _i x. y ) ) ) $.
    cnrehmeo.2 $e |- J = ( topGen ` ran (,) ) $.
    cnrehmeo.3 $e |- K = ( MetOpen ` ( abs o. - ) ) $.
    $( The canonical bijection from ` ( RR X. RR ) ` to ` CC ` described in
       ~ cnref1o is in fact a homeomorphism of the usual topologies on these
       sets.  (It is also an isometry, if ` ( RR X. RR ) ` is metrized with the
       l<SUP>2</SUP> norm.) $)
    cnrehmeo $p |- F e. ( ( J tX J ) Homeo K ) $=
      ( vz co wcel wtru ccn cr cc cfv a1i ax-mp cre cim ctx chomeosm ccnv cv ci
      cmul caddc cmpt2 cioo crn ctg cuni uniretop unieqi eqtr4i ctop retop cabs
      eqeltri cmin ccom cme eqid cnmet opntop wss cop csubsp ax-resscn cnmpt1st
      txtopi tgioo2 eqtri oveq2i syl6eleq wceq cnmetba uniopn2 cnsubsp2r ax-icn
      syl22anc cnmpt2c cnmpt2nd mulcn cnmpt22f syl5eqel cmpt cnrecnv wfn wf ref
      addcn ffn dffn5v mpbi ccncf recncf ssid cxp cres absf fnfco mp2an fnresdm
      subopr eqcomi tgioo cncfmet eleqtri eqeltrri imf imcncf cnmpt1t wa hmeobc
      copn wb sylanbrc trud ) CDDUAJZEUBJKZLCXTEMJZKZCUCZEXTMJZKZYALCABNNAUDZUE
      BUDZUFJZUGJUHYBFLABYGYIUGDDEEEONNONUIUJUKPZULDULUMDYJGUNUOZYKDUPKLDYJUPGU
      QUSZQZYMEUPKZLURUTVAZVBKZYNYOYOVCZVDZYOEHVERZQZYTLXTUPKZYNNOVFZABNNYGUHZX
      TNEVGVHPZMJZKUUCYBKUUALDDYLYLVKZQZYTUUBLVIQZLUUCXTDMJZUUELABDDNNYKYKYMYMV
      JDUUDXTMDYJUUDGEHVLVMVNZVONUUCXTEOYPOEULVPYRYOEOYOYQVQHVRRZVSWALABUEYHUFD
      DEEEONNOYKYKYMYMYTYTLABUEDDENNOYKYKYMYMYTUUKUEOKLVTQWBLUUAYNUUBABNNYHUHZU
      UEKUULYBKUUGYTUUHLUULUUIUUELABDDNNYKYKYMYMWCUUJVONUULXTEOUUKVSWAUUKUUKYTU
      FEEUAJEMJZKLYOEYQHWDQWEUUKUUKYTUGUUMKLYOEYQHWLQWEWFLYDIOIUDZSPZUUNTPZVGWG
      YEABICFWHLIUUOUUPEDDONUUKYTYKYMYMIOUUOWGZEDMJZKLSUUQUURSOWIZSUUQVPONSWJUU
      SWKONSWMRIOSWNWOSONWPJZUURWQOOVFUUBUUTUURVPOWRVIONYOYONNWSWTZEDYOOOWSZWTZ
      YOYOUVBWIZUVCYOVPUROWIZUVBOUTWJUVDONURWJUVEXAONURWMRXEOUVBURUTXBXCUVBYOXD
      RXFUVAVCZHDYJUVAXPPZGUVAUVGUVFUVGVCXGVMXHXCZXIXJQIOUUPWGZUURKLTUVIUURTOWI
      ZTUVIVPONTWJUVJXKONTWMRIOTWNWOTUUTUURXLUVHXIXJQXMWFUUAYNYAYCYFXNXQUUFYSCX
      TEXOXCXRXS $.
      $( [25-Aug-2014] $)
  $}

