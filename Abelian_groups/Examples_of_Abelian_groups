$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Examples of Abelian groups
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Complex number addition is an Abelian group operation. $)
    cnaddabl $p |- + e. Abel $=
      ( vx vy vz caddc cc cc0 cv cneg axcnex axaddopr axaddass 0cn addid2t
      negclt wcel co wceq axaddcom mpdan negidt eqtr3d isgrpi isabli ) ABDEABCF
      DAGZHZEIJUDBGZCGKLUDMUDNZUDEOZUDUEDPZUEUDDPZFUHUEEOUIUJQUGUDUERSUDTUAUBJU
      DUFRUC $.
      $( [7-Nov-2006] $) $( [5-Nov-2006] $)
  $}

  ${
    $d u x y z A $.
    ablsn.1 $e |- A e. V $.
    $( The Abelian group operation for the singleton group. $)
    ablsn $p |- { <. <. A , A >. , A >. } e. Abel $=
      ( vx vy vz cop csn snex cxp wf wf1o opex f1osn f1of ax-mp wceq wb xpsn
      feq2 mpbir cv co wcel w3a wa opreq2 opreq1 cfv df-opr fvsn eqtr syl6eq
      sylan9eq opreq1d sylan9eqr 3impa opreq2d 3impb eqtr4d elsn syl3anb snid
      id 3eqtr4a sylbi a1i isgrpi opreq12 syl2anb isabli ) CDAAFZAFGZAGZCDEAVLA
      VMAHVMVMIZVMVLJZVKGZVMVLJZVPVMVLKVQVKAAALZBMVPVMVLNOVNVPPVOVQQAABBRVNVPVM
      VLSOTZCUAZAPZDUAZAPZEUAZAPZVTWBVLUBZWDVLUBZVTWBWDVLUBZVLUBZPVTVMUCZWBVMUC
      ZWDVMUCWAWCWEUDWGAWIWAWCWEWGAPWEWAWCUEZWGWFAVLUBZAWDAWFVLUFWLWMAAVLUBZAWL
      WFAAVLWAWCWFAWBVLUBZAVTAWBVLUGWCWOWNAWBAAVLUFWNVKVLUHAAAVLUIVKAVRBUJUKZUL
      UMUNWPULUOUPWAWCWEWIAPWAWCWEUEZWIAWHVLUBZAVTAWHVLUGWQWRWNAWQWHAAVLWCWEWHA
      WDVLUBZAWBAWDVLUGWEWSWNAWDAAVLUFWPULUMUQWPULUMURUSCAUTZDAUTZEAUTVAABVBZWJ
      WAAVTVLUBZVTPWTWAWNAXCVTWPVTAAVLUFZWAVCVDVEAVMUCWJXBVFWJWAXCAPWTWAXCWNAXD
      WPULVEVGVSWAWCWFWBVTVLUBZPWJWKWLWFWNXEVTAWBAVLVHWAWCXEWBAVLUBWNVTAWBVLUFW
      BAAVLUGUMUSWTXAVIVJ $.
      $( [29-Nov-2006] $) $( [5-Nov-2006] $)
  $}

  ${
    $d u x $.
    $( The group identity element of complex number addition is zero.
       (Contributed by Steve Rodriguez, 3-Dec-2006.) $)
    cnid $p |- ( Id ` + ) = 0 $=
      ( vx vu cc0 cv caddc co wceq cc wral cgi cfv addid2t rgen wcel wb 0cn
      crab cuni opreq1 eqeq1d ralbidv cgr cabl cnaddabl ablgrp ax-mp axaddopr
      grprn eqid grpidval eqeq12 eqcom sylanb mpan bibi12d wreu grpideu
      reuuni1 mpan2 vtoclga mpbi ) CADZEFZVBGZAHIZEJKZCGZVDAHVBLMCHNVEVGOZPBDZV
      BEFZVBGZAHIZVLBHQRZVIGZOZVHBCHVICGZVLVEVNVGVPVKVDAHVPVJVCVBVICVBESTUAVFVM
      GZVPVNVGOZEUBNZVQEUCNVSUDEUEUFZABVFEHEHVTUGUHZVFUIUJUFVMVFGVPVRVQVMVFVICU
      KVFVMULUMUNUOVIHNVLBHUPZVOVSWBVTABEHWAUQUFVLBHURUSUTUFVA $.
      $( [4-Dec-2006] $) $( [28-Nov-2006] $)
  $}

  ${
    $d y A $.
    $( Value of the group inverse of complex number addition.  (Contributed by
       Steve Rodriguez, 3-Dec-2006.) $)
    addinv $p |- ( A e. CC -> ( ( inv ` + ) ` A ) = -u A ) $=
      ( vy cc wcel caddc cgn cfv cc0 cmin co cneg cv wceq crab cuni wa
      axaddcom eqeq1d rabbidv unieqd c1 cif opreq2 opreq1 rabbisdv eqeq12d 0cn
      ax1cn elimel subval dedth cgr cabl cnaddabl ablgrp ax-mp axaddopr grprn
      cgi cnid eqcomi eqid grpinvval mpan 3eqtr4rd df-neg syl6eqr ) ACDZAEFGZGZ
      HAIJZAKVHABLZEJZHMZBCNZOZVLAEJZHMZBCNZOZVKVJVHVOVSVHVNVRBCVHVLCDPVMVQHAVL
      QRSTVHVKVPMHVHAUAUBZIJZWAVLEJZHMZBCNZOZMAUAAWAMZVKWBVPWFAWAHIUCWGVOWEWGVN
      WDBCWGVMWCHAWAVLEUDRUETUFBHWAUGAUACUHUIUJUKEULDZVHVJVTMEUMDWHUNEUOUPZBAHE
      VICECWIUQUREUSGHUTVAVIVBVCVDVEAVFVG $.
      $( [4-Dec-2006] $) $( [3-Dec-2006] $)
  $}

  ${
    $d x y z $.
    $( Nonzero complex number multiplication is an Abelian group operation.
       (Contributed by Steve Rodriguez, 12-Feb-2007.) $)
    ablmul $p |- ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) e. Abel $=
      ( vx vy vz cmul cc cc0 csn cdif cxp cres c1 cv cdiv co cvv wcel axcnex
      difexg ax-mp mulnzcnopr w3a wa wceq oprvalres wne axmulcl ad2ant2r
      muln0bt biimpa an4s jca wn eldif elsn negbii df-ne bitr4 anbi2i bitr
      syl2anb 0cn elisseti elsnc2 sylibr eqeltrd anim1i 3impa syl 3adant3
      opreq1d eqtrd axmulass eldifi syl3an eqcomd 3adant1 opreq2d cop cfv
      visset opelxp ffvrni sylbir df-opr syl5eqel eqeltrrd sylan2 3impb
      3eqtr3d 3eqtrd ax1cn ax1ne0 nemtbir mpbir2an mpan mulid2t recclt recne0t
      3imtr4 mpancom recid2t sylbi isgrpi axmulcom syl2an ancoms 3eqtr4d
      isabli ) ABDEFGZHZXJIZJZXJABCKXLKALZMNZXJEOPXJOPQEXIORSTXMXJPZBLZXJPZCLZX
      JPZUAZXMXPXLNZXRXLNZXMXPDNZXRDNZXMXPXRDNZDNZXMXPXRXLNZXLNZXTYBYAXRDNZYDXT
      YAXJPZXSUBZYBYIUCXOXQXSYKXOXQUBZYJXSYLYAYCXJXMXPXJXJDUDZYLYCEPZYCFUEZUBZY
      CXJPZXMEPZXMFUEZUBZXPEPZXPFUEZUBZYPXOXQYTUUCUBYNYOYRUUAYNYSUUBXMXPUFUGYRU
      UAYSUUBYOYRUUAUBYSUUBUBYOXMXPUHUIUJUKXOYRXMXIPZULZUBYTXMEXIUMUUEYSYRUUEXM
      FUCZULYSUUDUUFAFUNUOXMFUPUQURUSZXQUUAXPXIPZULZUBUUCXPEXIUMUUIUUBUUAUUIXPF
      UCZULUUBUUHUUJBFUNUOXPFUPUQURUSUTYQYNYCXIPZULZUBYPYCEXIUMUULYOYNUULYCFUCZ
      ULYOUUKUUMYCFFEVAVBZVCUOYCFUPUQURUSVDVEVFVGYAXRXJXJDUDVHXTYAYCXRDXOXQYAYC
      UCXSYMVIVJVKYRUUAXREPYDYFUCXOXQXSXMXPXRVLXMEXIVMZXPEXIVMZXREXIVMVNXTYFXMY
      GDNZYHXTYEYGXMDXQXSYEYGUCXOXQXSUBZYGYEXPXRXJXJDUDZVOVPZVQZXTYFXMYEXLNZUUQ
      YHXOXQXSYFUVBUCZXOYEXJPZUVCUURXOUVDUBUVBYFXMYEXJXJDUDVOUURYGYEXJUUSUURXPX
      RVRZXLVSZXJYGUURUVEXKPUVFXJPXPXRXJXJCVTWAXKXJUVEXLTWBWCXPXRXLWDWEWFWGWHUV
      AXTYEYGXMXLUUTVQWIVKWJKXJPZKEPKXIPZULKEXIUMWKUVHKFWLKFUUNVCWMWNZXOKXMXLNZ
      KXMDNZXMUVGXOUVJUVKUCUVIKXMXJXJDUDWOXOYRUVKXMUCUUOXMWPVHVKYTXNEPZXNFUEZUB
      ZXOXNXJPZYTUVLUVMXMWQXMWRUKUUGUVOUVLXNXIPZULZUBUVNXNEXIUMUVMUVQUVLUVMXNFU
      CZULUVQXNFUPUVPUVRXNFUUNVCUOUQURUQWSZXOXNXMXLNZXNXMDNZKUVOXOUVTUWAUCUVSXN
      XMXJXJDUDWTXOYTUWAKUCUUGXMXAXBVKXCTYLYCXPXMDNZYAXPXMXLNZYRUUAYCUWBUCXOXQX
      MXPXDUUOUUPXEYMXQXOUWCUWBUCXPXMXJXJDUDXFXGXH $.
      $( [24-Feb-2007] $) $( [7-Nov-2006] $)
  $}

  ${
    $d u x $.
    $( The group identity element of nonzero complex number multiplication is
       one.  (Contributed by Steve Rodriguez, 23-Feb-2007.) $)
    mulid $p |- ( Id ` ( x. |` ( ( CC \ { 0 } ) X. ( CC \ { 0 } ) ) ) ) = 1 $=
      ( vx vu c1 cv cmul cc cc0 csn cdif cxp cres co wceq wral cgi cfv wcel
      wne wn wa eldif df-ne 0cn elisseti elsnc2 negbii bitr4 anbi2i ax1cn
      ax1ne0 mpbir2an oprvalres mpan eldifi mulid2t syl eqtrd rgen wb crab
      cuni opreq1 eqeq1d ralbidv cgr cabl ablmul ablgrp ax-mp mulnzcnopr grprn
      eqid grpidval eqeq12 eqcom sylanb bibi12d wreu grpideu reuuni1 mpan2
      vtoclga mpbi ) CADZEFGHZIZWFJKZLZWDMZAWFNZWGOPZCMZWIAWFWDWFQZWHCWDELZWDCW
      FQZWMWHWNMWOCFQZCGRZWOWPCWEQZSZTWPWQTCFWEUAWQWSWPWQCGMZSWSCGUBWRWTCGGFUCU
      DUEUFUGUHUGUIUJUKZCWDWFWFEULUMWMWDFQWNWDMWDFWEUNWDUOUPUQURWOWJWLUSZXABDZW
      DWGLZWDMZAWFNZXFBWFUTVAZXCMZUSZXBBCWFXCCMZXFWJXHWLXJXEWIAWFXJXDWHWDXCCWDW
      GVBVCVDWKXGMZXJXHWLUSZWGVEQZXKWGVFQXMVGWGVHVIZABWKWGWFWGWFXNVJVKZWKVLVMVI
      XGWKMXJXLXKXGWKXCCVNWKXGVOVPUMVQXCWFQXFBWFVRZXIXMXPXNABWGWFXOVSVIXFBWFVTW
      AWBVIWC $.
      $( [24-Feb-2007] $) $( [3-Dec-2006] $)
  $}


