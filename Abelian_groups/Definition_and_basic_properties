$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Abel $.

  $( Extend class notation with the class of all Abelian group operations. $)
  cabl $a class Abel $.

  ${
    $d g x y $.
    $( Define the class of all Abelian group operations. $)
    df-abl $a |- Abel = { g e. Grp | A. x e. ran g A. y e. ran g ( x G y ) =
                 ( y G x ) } $.
  $}

  ${
    $d g x y G $.  $d g x y X $.
    isabl.1 $e |- X = ran G $.
    $( The predicate "is an Abelian (commutative) group operation" $)
    isabl $p |- ( G e. Abel <-> ( G e. Grp /\
             A. x e. X A. y e. X ( x G y ) = ( y G x ) ) ) $=
      ( vg cv co wceq crn wral cgr cabl wb rneq syl6eqr raleq1 raleqd syl
      opreq eqeq12d 2ralbidv bitrd df-abl elrab2 ) AGZBGZFGZHZUGUFUHHZIZBUHJZKZ
      AULKZUFUGCHZUGUFCHZIZBDKADKZFCLMUHCIZUNUKBDKZADKZURUSULDIUNVANUSULCJDUHCO
      EPUMUTAULDUKBULDQRSUSUKUQABDDUSUIUOUJUPUFUGUHCTUGUFUHCTUAUBUCABFUHUDUE $.
      $( [4-Nov-2006] $) $( [2-Nov-2006] $)
  $}

  ${
    $d x y G $.
    $( An Abelian group operation is a group operation. $)
    ablgrp $p |- ( G e. Abel -> G e. Grp ) $=
      ( vx vy cabl wcel cgr cv co wceq crn wral eqid isabl pm3.26bd ) ADEAFEBGZ
      CGZAHPOAHICAJZKBQKBCAQQLMN $.
      $( [6-Nov-2006] $) $( [2-Nov-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y G $.  $d x y X $.
    ablcom.1 $e |- X = ran G $.
    $( An Abelian group operation is commutative. $)
    ablcom $p |- ( ( G e. Abel /\ A e. X /\ B e. X ) ->
                 ( A G B ) = ( B G A ) ) $=
      ( vx vy cabl wcel co wceq wa cv wral opreq1 opreq2 eqeq12d rcla42v cgr
      isabl pm3.27bd syl5com 3impib ) CHIZADIZBDIZABCJZBACJZKZUEUFLFMZGMZCJZUKU
      JCJZKZGDNFDNZUIUDUNUIAUKCJZUKACJZKFGABDDUJAKULUPUMUQUJAUKCOUJAUKCPQUKBKUP
      UGUQUHUKBACPUKBACOQRUDCSIUOFGCDETUAUBUC $.
      $( [10-Nov-2006] $) $( [2-Nov-2006] $)

    $( Commutative/associative law for Abelian groups. $)
    abl23 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                 ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cabl wcel w3a wa co wceq ablcom 3expb 3adantr1 opreq2d cgr grpass
      ablgrp sylan 3ancomb sylan2b 3eqtr4d ) DGHZAEHZBEHZCEHZIZJZABCDKZDKZACBDK
      ZDKZABDKCDKZACDKBDKZUIUJULADUDUFUGUJULLZUEUDUFUGUPBCDEFMNOPDQHZUHUNUKLUDA
      BCDEFRDSZTUQUHUOUMLZUDUQUEUGUFIUSUHACBDEFRUEUFUGUAUBURTUC $.
      $( [26-Apr-2007] $) $( [26-Apr-2007] $)

    $( Commutative/associative law for Abelian groups. $)
    abl4 $p |- ( ( G e. Abel /\ ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X ) )
               -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cabl wcel wa w3a co wceq abl23 3simp1 id 3expa adantrr 3adant1 sylanc
      opreq1d cgr grpass ablgrp 3ad2ant1 grpcl syl3an1 3expb 3adant3 simprl
      simprr 3jca adantrlr adantrrr 3impb simplr 3eqtr3d ) EHIZAFIZBFIZJZCFIZDF
      IZJZKZABELZCELZDELZACELZBELZDELZVFCDELELZVIBDELELZVEVGVJDEURUSUTVBKZVGVJM
      VEABCEFGNURVAVDOVAVDVNURVAVBVNVCUSUTVBVNVNPQRSTUAEUBIZVFFIZVBVCKVHVLMVEVF
      CDEFGUCURVAVOVDEUDZUEZVEVPVBVCURVAVPVDURUSUTVPVOUSUTVPURABEFGUFVQUGUHUIVA
      VDVBURVAVBVCUJSVAVDVCURVAVBVCUKSZULTVOVIFIZUTVCKVKVMMVEVIBDEFGUCVRVEVTUTV
      CURVAVDVTURVAVBVTVCURUSVBVTUTURUSVBVTVOUSVBVTURACEFGUFVQUGUHUMUNUOVAVDUTU
      RUSUTVDUPSVSULTUQ $.
      $( [?] $) $( [26-Apr-2007] $)
  $}

  ${
    $d x y G $.  $d x y X $.
    isabli.1 $e |- G e. Grp $.
    isabli.2 $e |- G : ( X X. X ) --> X $.
    isabli.3 $e |- ( ( x e. X /\ y e. X ) -> ( x G y ) = ( y G x ) ) $.
    $( Properties that determine an Abelian group operation. $)
    isabli $p |- G e. Abel $=
      ( cabl wcel cgr cv co wceq wral grprn isabl rgen2 mpbir2an ) CHICJIAKZBKZ
      CLTSCLMZBDNADNABCDCDEFOPEUAABDGQR $.
      $( [7-Nov-2006] $) $( [5-Nov-2006] $)
  $}

