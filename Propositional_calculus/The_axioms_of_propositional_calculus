$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The axioms of propositional calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $(
     Postulate the three axioms of classical propositional calculus.  See,
     for example, Definition 2.1, p. 28, of Hamilton.
  $)

  $( Axiom of Simplification.
     One of the 3 axioms of propositional calculus.
     Informally, propositional calculus can be thought of as describing
     formulas that are universally "true" when their variables are replaced
     by any combination of "true" and "false".  This axiom was called
     "the principle of simplification" by Whitehead and Russell (Theorem
     *2.02 in _Principia Mathematica_) because "it enables us to pass from
     the joint assertion of ` ph ` and ` ps ` to the assertion of ` ph `
     simply." $)
  ax-1 $a |- ( ph -> ( ps -> ph ) ) $.

  $( Axiom of Distribution.
     One of the 3 axioms of propositional calculus.  It distributes an
     antecedent over two consequents. $)
  ax-2 $a |- ( ( ph -> ( ps -> ch ) ) -> ( ( ph -> ps ) -> ( ph -> ch ) ) ) $.

  $( Axiom of Contraposition.
     One of the 3 axioms of propositional calculus.  It swaps or contraposes
     the order of the consequents when negation is removed. $)
  ax-3 $a |- ( ( -. ph -> -. ps ) -> ( ps -> ph ) ) $.

  $(
     Postulate the modus ponens rule of inference.
  $)

  ${
    $( Minor premise for modus ponens. $)
    min   $e |- ph $.
    $( Major premise for modus ponens. $)
    maj   $e |- ( ph -> ps ) $.
    $( Rule of Modus Ponens.
       The postulated inference rule of propositional calculus.  It says,
       "if ` ph ` is true, and ` ph ` implies ` ps `, then ` ps ` must
       also be true." $)
    ax-mp $a |- ps $.
  $}

  $(
     ~ ax-1 , ~ ax-2 , ~ ax-3 , and ~ ax-mp are the complete set of
     postulates for propositional calculus.  Some additional $a statements
     are used later to introduce defined terms.
  $)

