$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Logical equivalence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$( This is our first definition.  It introduces the biconditional
   connective.  Unlike most traditional developments, we have chosen not
   to have a separate symbol such as 'Df.' to mean 'is defined as.'  Instead,
   we will use the biconditional connective for this purpose, as it allows us
   to use logic to manipulate definitions directly.  Here we use the
   biconditional connective to define itself, a feat which requires a
   little care and is done by three $a statements.  Eventually we derive
   theorem ~ bii , which is similar to the standard textbook definition and
   enables us to eliminate the definition.  (You may wish to view these
   three definitions as additional axioms if you want to minimize the
   metatheory involved.) $)

  $( Declare the biconditional connective. $)
  $c <-> $. $( Double arrow (read:  'if and only if' or
               'is logically equivalent to') $)

  $( Extend our wff definition to include the biconditional connective. $)
  wb    $a wff ( ph <-> ps ) $.

  $( Define the biconditional connective.  We define ` ( ph <-> ps ) ` as
     ` -. ( ( ph -> ps ) -> -. ( ps -> ph ) ) ` but we don't yet have a
     notation that can express this directly, so we use a more complicated
     expression using the language that we have so far.  The justification for
     our definition is that if we mechanically substitute the second wff for
     the first in the definition, the definition becomes an instance of
     previously proved theorem ~ bijust .  Later, we will make extensive use of
     the biconditional connective to make definitions more compact -- see
     ~ df-or for its first use.  Note that from Metamath's point of view, a
     definition is just another axiom; but from our higher-level point of
     view, we are are not strengthening the language since if we mechanically
     eliminate the new symbol ` <-> ` we will not end up with any theorems
     we could not prove before.   To indicate this fact we prefix definition
     labels with "df-" instead of "ax-".  Note:  See ~ bi for a theorem
     suggesting the typical textbook definition of ` <-> `. $)
  df-bi $a |- -. ( ( ( ph <-> ps ) -> -. ( ( ph -> ps ) -> -. ( ps -> ph ) ) )
        -> -. ( -. ( ( ph -> ps ) -> -. ( ps -> ph ) ) -> ( ph <-> ps ) ) ) $.

  $( Property of the biconditional connective. $)
  bi1 $p |- ( ( ph <-> ps ) -> ( ph -> ps ) ) $=
    ( wb wi wn df-bi pm3.26im ax-mp syl ) ABCZABDZBADZEDEZKJMDZMJDZEDENABFNOGHK
    LGI $.
    $([11-May-99]$)

  $( Property of the biconditional connective. $)
  bi2 $p |- ( ( ph <-> ps ) -> ( ps -> ph ) ) $=
    ( wb wi wn df-bi pm3.26im ax-mp pm3.27im syl ) ABCZABDZBADZEDEZMKNDZNKDZEDE
    OABFOPGHLMIJ $.
    $([11-May-99]$)

  $( Property of the biconditional connective. $)
  bi3 $p |- ( ( ph -> ps ) -> ( ( ps -> ph ) -> ( ph <-> ps ) ) ) $=
    ( wi wb wn df-bi pm3.27im ax-mp expi ) ABCZBACZABDZLJKECEZCZMLCZECEOABFNOGH
    I $.
    $([11-May-99]$)

  ${
    biimp.1 $e |- ( ph <-> ps ) $.
    $( Infer an implication from a logical equivalence. $)
    biimp   $p |- ( ph -> ps ) $=
      ( wb wi bi1 ax-mp ) ABDABECABFG $.
      $([5-Aug-93]$)
  $}

  ${
    biimpr.1 $e |- ( ph <-> ps ) $.
    $( Infer a converse implication from a logical equivalence. $)
    biimpr   $p |- ( ps -> ph ) $=
      ( wb wi bi2 ax-mp ) ABDBAECABFG $.
      $([5-Aug-93]$)
  $}

  ${
    biimpd.1 $e |- ( ph -> ( ps <-> ch ) ) $.

    $( Deduce an implication from a logical equivalence. $)
    biimpd   $p |- ( ph -> ( ps -> ch ) ) $=
      ( wb wi bi1 syl ) ABCEBCFDBCGH $.
      $([5-Aug-93]$)

   $( Deduce a converse implication from a logical equivalence. $)
    biimprd  $p |- ( ph -> ( ch -> ps ) ) $=
      ( wb wi bi2 syl ) ABCECBFDBCGH $.
      $([5-Aug-93]$)

   $( Deduce a commuted implication from a logical equivalence. $)
    biimpcd  $p |- ( ps -> ( ph -> ch ) ) $=
      ( biimpd comm ) ABCABCDEF $.
      $([3-May-94]$)

   $( Deduce a converse commuted implication from a logical equivalence. $)
    biimprcd $p |- ( ch -> ( ph -> ps ) ) $=
      ( biimprd comm ) ACBABCDEF $.
      $([3-May-94]$)
  $}

  ${
    impbi.1 $e |- ( ph -> ps ) $.
    impbi.2 $e |- ( ps -> ph ) $.
    $( Infer an equivalence from an implication and its converse. $)
    impbi   $p |- ( ph <-> ps ) $=
      ( wi wb bi3 mp2 ) ABEBAEABFCDABGH $.
      $([5-Aug-93]$)
  $}

  $( Relate the biconditional connective to primitive connectives. $)
  bii $p |- ( ( ph <-> ps ) <-> -. ( ( ph -> ps ) -> -. ( ps -> ph ) ) ) $=
    ( wb wi wn bi1 bi2 jc bi3 impi impbi ) ABCZABDZBADZEDELMNABFABGHMNLABIJK $.
    $([5-Aug-93]$)

  $( Logical equivalence of commuted antecedents.  Part of Theorem *4.87 of
     Whitehead and Russell. $)
  bi2.04 $p |- ( ( ph -> ( ps -> ch ) ) <-> ( ps -> ( ph -> ch ) ) ) $=
    ( wi pm2.04 impbi ) ABCDDBACDDABCEBACEF $.
    $([5-Aug-93]$)

  $( Double negation.  Theorem *4.13 of Whitehead and Russell. $)
  pm4.13 $p |- ( ph <-> -. -. ph ) $=
    ( wn negb nega impbi ) AABBACADE $.
    $([5-Aug-93]$)

  $( Contraposition.  Theorem *4.1 of Whitehead and Russell. $)
  pm4.1  $p |- ( ( ph -> ps ) <-> ( -. ps -> -. ph ) ) $=
    ( wi wn pm2.16 ax-3 impbi ) ABCBDADCABEBAFG $.
    $([5-Aug-93]$)

  $( Contraposition. $)
  bi2.03 $p |- ( ( ph -> -. ps ) <-> ( ps -> -. ph ) ) $=
    ( wn wi pm2.03 impbi ) ABCDBACDABEBAEF $.
    $([5-Aug-93]$)

  $( Contraposition. $)
  bi2.15 $p |- ( ( -. ph -> ps ) <-> ( -. ps -> ph ) ) $=
    ( wn wi pm2.15 impbi ) ACBDBCADABEBAEF $.
    $([5-Aug-93]$)

  $( Absorption law for implication.  Theorem *5.4 of Whitehead and
     Russell. $)
  pm5.4  $p |- ( ( ph -> ( ph -> ps ) ) <-> ( ph -> ps ) ) $=
    ( wi pm2.43 ax-1 impbi ) AABCZCGABDGAEF $.
    $([5-Aug-93]$)

  $( Distributive law for implication.  Theorem *5.41 of Whitehead and
     Russell. $)
  pm5.41 $p |- ( ( ph -> ( ps -> ch ) ) <->
               ( ( ph -> ps ) -> ( ph -> ch ) ) ) $=
    ( wi ax-2 pm2.86 impbi ) ABCDDABDACDDABCEABCFG $.
    $([5-Aug-93]$)

  $( Principle of identity for logical equivalence.  Theorem *4.2 of
     Whitehead and Russell. $)
  pm4.2  $p |- ( ph <-> ph ) $=
    ( id impbi ) AAABZDC $.
    $([5-Aug-93]$)

  $( Principle of identity with antecedent. $)
  pm4.2i  $p |- ( ph -> ( ps <-> ps ) ) $=
    ( wb pm4.2 a1i ) BBCABDE $.
    $([25-Nov-95]$)

  ${
    bicom.1 $e |- ( ph <-> ps ) $.
    $( Inference from commutative law for logical equivalence. $)
    bicom   $p |- ( ps <-> ph ) $=
      ( biimpr biimp impbi ) BAABCDABCEF $.
      $([5-Aug-93]$)
  $}

  ${
    bitr.1 $e |- ( ph <-> ps ) $.
    bitr.2 $e |- ( ps <-> ch ) $.
    $( An inference from transitive law for logical equivalence. $)
    bitr   $p |- ( ph <-> ch ) $=
      ( biimp syl biimpr impbi ) ACABCABDFBCEFGCBABCEHABDHGI $.
      $([5-Aug-93]$)
  $}

  ${
    bitr2.1 $e |- ( ph <-> ps ) $.
    bitr2.2 $e |- ( ps <-> ch ) $.
    $( An inference from transitive law for logical equivalence. $)
    bitr2   $p |- ( ch <-> ph ) $=
      ( bitr bicom ) ACABCDEFG $.
      $([5-Aug-93]$)
  $}

  ${
    bitr3.1 $e |- ( ps <-> ph ) $.
    bitr3.2 $e |- ( ps <-> ch ) $.
    $( An inference from transitive law for logical equivalence. $)
    bitr3   $p |- ( ph <-> ch ) $=
      ( bicom bitr ) ABCBADFEG $.
      $([5-Aug-93]$)
  $}

  ${
    bitr4.1 $e |- ( ph <-> ps ) $.
    bitr4.2 $e |- ( ch <-> ps ) $.
    $( An inference from transitive law for logical equivalence. $)
    bitr4   $p |- ( ph <-> ch ) $=
      ( bicom bitr ) ABCDCBEFG $.
      $([5-Aug-93]$)
  $}

  ${
    3bitr.1 $e |- ( ph <-> ps ) $.
    3bitr.2 $e |- ( ps <-> ch ) $.
    3bitr.3 $e |- ( ch <-> th ) $.
    $( A chained inference from transitive law for logical equivalence. $)
    3bitr   $p |- ( ph <-> th ) $=
      ( bitr ) ABDEBCDFGHH $.
      $([5-Aug-93]$)
  $}

  ${
    3bitr3.1 $e |- ( ph <-> ps ) $.
    3bitr3.2 $e |- ( ph <-> ch ) $.
    3bitr3.3 $e |- ( ps <-> th ) $.
    $( A chained inference from transitive law for logical equivalence. $)
    3bitr3   $p |- ( ch <-> th ) $=
      ( bitr3 bitr ) CBDCABFEHGI $.
      $([19-Aug-93]$)

    $( A chained inference from transitive law for logical equivalence. $)
    3bitr3r   $p |- ( th <-> ch ) $=
      ( bitr3 ) DBCGBACEFHH $.
      $([5-Aug-93]$)
  $}

  ${
    3bitr4.1 $e |- ( ph <-> ps ) $.
    3bitr4.2 $e |- ( ch <-> ph ) $.
    3bitr4.3 $e |- ( th <-> ps ) $.
    $( A chained inference from transitive law for logical equivalence.  This
       inference is frequently used to apply a definition to both sides of a
       logical equivalence. $)
    3bitr4   $p |- ( ch <-> th ) $=
      ( bitr4 bitr ) CADFABDEGHI $.
      $([5-Aug-93]$)

    $( A chained inference from transitive law for logical equivalence. $)
    3bitr4r   $p |- ( th <-> ch ) $=
      ( bitr4 bitr2 ) CADFABDEGHI $.
      $([2-Sep-95]$)
  $}

  $( The next three rules are useful for building up wff's around a
     definition, in order to make use of the definition. $)

  ${
    bi.a $e |- ( ph <-> ps ) $.

    $( Introduce an antecedent to both sides of a logical equivalence. $)
    bilim $p |- ( ( ch -> ph ) <-> ( ch -> ps ) ) $=
      ( wi biimp syl3 biimpr impbi ) CAECBEABCABDFGBACABDHGI $.
      $([5-Aug-93]$)

    $( Introduce a consequent to both sides of a logical equivalence. $)
    birim $p |- ( ( ph -> ch ) <-> ( ps -> ch ) ) $=
      ( wi biimpr syl4 biimp impbi ) ACEBCEBACABDFGABCABDHGI $.
      $([5-Aug-93]$)

    $( Negate both sides of a logical equivalence. $)
    bineg $p |- ( -. ph <-> -. ps ) $=
      ( wn biimpr con3 biimp impbi ) ADBDBAABCEFABABCGFH $.
      $([5-Aug-93]$)

  $}

  ${
    bi2im.1 $e |- ( ph <-> ps ) $.
    bi2im.2 $e |- ( ch <-> th ) $.
    $( Join two logical equivalences to form equivalence of implications. $)
    bi2im   $p |- ( ( ph -> ch ) <-> ( ps -> th ) ) $=
      ( wi bilim birim bitr ) ACGADGBDGCDAFHABDEIJ $.
      $([5-Aug-93]$)
  $}

  ${
    mpbi.min $e |- ph $.
    mpbi.maj $e |- ( ph <-> ps ) $.
    $( An inference from a biconditional, related to modus ponens. $)
    mpbi     $p |- ps $=
      ( biimp ax-mp ) ABCABDEF $.
      $([5-Aug-93]$)
  $}

  ${
    mpbir.min $e |- ps $.
    mpbir.maj $e |- ( ph <-> ps ) $.
    $( An inference from a biconditional, related to modus ponens. $)
    mpbir     $p |- ph $=
      ( biimpr ax-mp ) BACABDEF $.
      $([5-Aug-93]$)
  $}

  ${
    mtbi.1 $e |- -. ph $.
    mtbi.2 $e |- ( ph <-> ps ) $.
    $( An inference from a biconditional, related to modus tollens. $)
    mtbi   $p |- -. ps $=
      ( wn bineg mpbi ) AEBECABDFG $.
      $([15-Nov-94]$)
  $}

  ${
    mtbir.1 $e |- -. ps $.
    mtbir.2 $e |- ( ph <-> ps ) $.
    $( An inference from a biconditional, related to modus tollens. $)
    mtbir   $p |- -. ph $=
      ( wn bineg mpbir ) AEBECABDFG $.
      $([15-Nov-94]$)
  $}

  ${
    mpbii.min $e |- ps $.
    mpbii.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( An inference from a nested biconditional, related to modus ponens. $)
    mpbii     $p |- ( ph -> ch ) $=
      ( biimpd mpi ) ABCDABCEFG $.
      $([5-Aug-93]$)
  $}

  ${
    mpbiri.min $e |- ch $.
    mpbiri.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( An inference from a nested biconditional, related to modus ponens. $)
    mpbiri     $p |- ( ph -> ps ) $=
      ( biimprd mpi ) ACBDABCEFG $.
      $([5-Aug-93]$)
  $}

  ${
    mpbid.min $e |- ( ph -> ps ) $.
    mpbid.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( A deduction from a biconditional, related to modus ponens. $)
    mpbid     $p |- ( ph -> ch ) $=
      ( biimpd mpd ) ABCDABCEFG $.
      $([5-Aug-93]$)
  $}

  ${
    mpbird.min $e |- ( ph -> ch ) $.
    mpbird.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( A deduction from a biconditional, related to modus ponens. $)
    mpbird     $p |- ( ph -> ps ) $=
      ( biimprd mpd ) ACBDABCEFG $.
      $([5-Aug-93]$)
  $}

  ${
    a1bi.1 $e |- ph $.
    $( Inference rule introducing a theorem as an antecedent. $)
    a1bi   $p |- ( ps <-> ( ph -> ps ) ) $=
      ( wi ax-1 pm2.27 ax-mp impbi ) BABDZBAEAIBDCABFGH $.
      $([5-Aug-93]$)
  $}

  ${
    sylib.1 $e |- ( ph -> ps ) $.
    sylib.2 $e |- ( ps <-> ch ) $.
    $( A mixed syllogism inference from an implication and a biconditional. $)
    sylib   $p |- ( ph -> ch ) $=
      wph wps wch sylib.1 wps wch sylib.2 biimp syl $.
      $([5-Aug-93]$)
  $}

  ${
    sylbi.1 $e |- ( ph <-> ps ) $.
    sylbi.2 $e |- ( ps -> ch ) $.
    $( A mixed syllogism inference from a biconditional and an implication.
       Useful for substituting an antecedent with a definition. $)
    sylbi   $p |- ( ph -> ch ) $=
      wph wps wch wph wps sylbi.1 biimp sylbi.2 syl $.
      $([5-Aug-93]$)
  $}

  ${
    sylibr.1 $e |- ( ph -> ps ) $.
    sylibr.2 $e |- ( ch <-> ps ) $.
    $( A mixed syllogism inference from an implication and a biconditional.
       Useful for substituting a consequent with a definition. $)
    sylibr   $p |- ( ph -> ch ) $=
      wph wps wch sylibr.1 wch wps sylibr.2 biimpr syl $.
      $([5-Aug-93]$)
  $}

  ${
    sylbir.1 $e |- ( ps <-> ph ) $.
    sylbir.2 $e |- ( ps -> ch ) $.
    $( A mixed syllogism inference from a biconditional and an implication. $)
    sylbir   $p |- ( ph -> ch ) $=
      wph wps wch wps wph sylbir.1 biimpr sylbir.2 syl $.
      $([5-Aug-93]$)
  $}

  ${
    sylibd.1 $e |- ( ph -> ( ps -> ch ) ) $.
    sylibd.2 $e |- ( ph -> ( ch <-> th ) ) $.
    $( A syllogism deduction. $)
    sylibd   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth sylibd.1 wph wch wth sylibd.2 biimpd syld $.
      $([3-Aug-94]$)
  $}

  ${
    sylbid.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    sylbid.2 $e |- ( ph -> ( ch -> th ) ) $.
    $( A syllogism deduction. $)
    sylbid   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth wph wps wch sylbid.1 biimpd sylbid.2 syld $.
      $([3-Aug-94]$)
  $}

  ${
    sylibrd.1 $e |- ( ph -> ( ps -> ch ) ) $.
    sylibrd.2 $e |- ( ph -> ( th <-> ch ) ) $.
    $( A syllogism deduction. $)
    sylibrd   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth sylibrd.1 wph wth wch sylibrd.2 biimprd syld $.
      $([3-Aug-94]$)
  $}

  ${
    sylbird.1 $e |- ( ph -> ( ch <-> ps ) ) $.
    sylbird.2 $e |- ( ph -> ( ch -> th ) ) $.
    $( A syllogism deduction. $)
    sylbird   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth wph wch wps sylbird.1 biimprd sylbird.2 syld $.
      $([3-Aug-94]$)
  $}

  ${
    syl5ib.1 $e |- ( ph -> ( ps -> ch ) ) $.
    syl5ib.2 $e |- ( th <-> ps ) $.
    $( A mixed syllogism inference from a nested implication and a
       biconditional.  Useful for substituting an embedded antecedent with a
       definition. $)
    syl5ib   $p |- ( ph -> ( th -> ch ) ) $=
      wph wps wch wth syl5ib.1 wth wps syl5ib.2 biimp syl5 $.
      $([5-Aug-93]$)
  $}

  ${
    syl5ibr.1 $e |- ( ph -> ( ps -> ch ) ) $.
    syl5ibr.2 $e |- ( ps <-> th ) $.
    $( A mixed syllogism inference from a nested implication and a
       biconditional. $)
    syl5ibr   $p |- ( ph -> ( th -> ch ) ) $=
      wph wps wch wth syl5ibr.1 wps wth syl5ibr.2 biimpr syl5 $.
      $([5-Aug-93]$)
  $}

  ${
    syl5bi.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    syl5bi.2 $e |- ( th -> ps ) $.
    $( A mixed syllogism inference. $)
    syl5bi   $p |- ( ph -> ( th -> ch ) ) $=
      wph wps wch wth wph wps wch syl5bi.1 biimpd syl5bi.2 syl5 $.
      $([5-Aug-93]$)
  $}

  ${
    syl5bir.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    syl5bir.2 $e |- ( th -> ch ) $.
    $( A mixed syllogism inference. $)
    syl5bir   $p |- ( ph -> ( th -> ps ) ) $=
      wph wch wps wth wph wps wch syl5bir.1 biimprd syl5bir.2 syl5 $.
      $([3-Apr-94]$)
  $}

  ${
    syl6ib.1 $e |- ( ph -> ( ps -> ch ) ) $.
    syl6ib.2 $e |- ( ch <-> th ) $.
    $( A mixed syllogism inference from a nested implication and a
       biconditional. $)
    syl6ib   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth syl6ib.1 wch wth syl6ib.2 biimp syl6 $.
      $([5-Aug-93]$)
  $}

  ${
    syl6ibr.1 $e |- ( ph -> ( ps -> ch ) ) $.
    syl6ibr.2 $e |- ( th <-> ch ) $.
    $( A mixed syllogism inference from a nested implication and a
       biconditional.  Useful for substituting an embedded consequent with a
       definition. $)
    syl6ibr   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth syl6ibr.1 wth wch syl6ibr.2 biimpr syl6 $.
      $([5-Aug-93]$)
  $}


  ${
    syl6bi.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    syl6bi.2 $e |- ( ch -> th ) $.
    $( A mixed syllogism inference. $)
    syl6bi   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth wph wps wch syl6bi.1 biimpd syl6bi.2 syl6 $.
      $([2-Jan-94]$)
  $}

  ${
    syl6bir.1 $e |- ( ph -> ( ch <-> ps ) ) $.
    syl6bir.2 $e |- ( ch -> th ) $.
    $( A mixed syllogism inference. $)
    syl6bir   $p |- ( ph -> ( ps -> th ) ) $=
      wph wps wch wth wph wch wps syl6bir.1 biimprd syl6bir.2 syl6 $.
      $([18-May-94]$)
  $}

  ${
    bisyl7.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    bisyl7.2 $e |- ( ta <-> ch ) $.
    $( A mixed syllogism inference from a doubly nested implication and a
       biconditional. $)
    bisyl7   $p |- ( ph -> ( ps -> ( ta -> th ) ) ) $=
      ( biimp syl7 ) ABCDEFECGHI $.
      $([5-Aug-93]$)
  $}

  ${
    bisyl8.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    bisyl8.2 $e |- ( th <-> ta ) $.
    $( A syllogism rule of inference.  The second premise is used to replace
       the consequent of the first premise. $)
    bisyl8   $p |- ( ph -> ( ps -> ( ch -> ta ) ) ) $=
      ( biimp syl8 ) ABCDEFDEGHI $.
      $([1-Aug-94]$)
  $}

  ${
    3imtr3.1 $e |- ( ph -> ps ) $.
    3imtr3.2 $e |- ( ph <-> ch ) $.
    3imtr3.3 $e |- ( ps <-> th ) $.
    $( A mixed syllogism inference, useful for removing a definition from both
       sides of an implication. $)
    3imtr3   $p |- ( ch -> th ) $=
      ( sylbir sylib ) CBDCABFEHGI $.
      $([10-Aug-94]$)
  $}

  ${
    3imtr4.1 $e |- ( ph -> ps ) $.
    3imtr4.2 $e |- ( ch <-> ph ) $.
    3imtr4.3 $e |- ( th <-> ps ) $.
    $( A mixed syllogism inference, useful for applying a definition to both
       sides of an implication. $)
    3imtr4   $p |- ( ch -> th ) $=
      ( sylbi sylibr ) CBDCABFEHGI $.
      $([5-Aug-93]$)
  $}

  ${
    bicon1.1 $e |- ( -. ph <-> ps ) $.
    $( A contraposition inference. $)
    bicon1   $p |- ( -. ps <-> ph ) $=
      ( wn biimp con1 biimpr con2 impbi ) BDAABADZBCEFBAJBCGHI $.
      $([5-Aug-93]$)
  $}

  ${
    bicon2.1 $e |- ( ph <-> -. ps ) $.
    $( A contraposition inference. $)
    bicon2   $p |- ( ps <-> -. ph ) $=
      ( wn bicom bicon1 ) ADBBAABDCEFE $.
      $([5-Aug-93]$)
  $}

  $( For ~ bicon3 :  See ~ bineg . $)

  $( For ~ bicon4 :  Later. $)


