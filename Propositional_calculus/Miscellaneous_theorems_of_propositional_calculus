$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Miscellaneous theorems of propositional calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two propositions are equivalent if they are both true.  Theorem *5.1 of
     Whitehead and Russell. $)
  pm5.1 $p |- ( ( ph /\ ps ) -> ( ph <-> ps ) ) $=
    ( wa pm3.4 pm3.26 a1d impbid ) ABCZABABDHABABEFG $.
    $([21-May-94]$)

  $( Two propositions are equivalent if they are both false.  Theorem *5.21 of
     Whitehead and Russell. $)
  pm5.21 $p |- ( ( -. ph /\ -. ps ) -> ( ph <-> ps ) ) $=
    ( wn wa pm5.1 bicon4d ) ACZBCZDABGHEF $.
    $([21-May-94]$)


  ${
    pm5.21ni.1 $e |- ( ph -> ps ) $.
    pm5.21ni.2 $e |- ( ch -> ps ) $.
    $( Two propositions implying a false one are equivalent. $)
    pm5.21ni   $p |- ( -. ps -> ( ph <-> ch ) ) $=
      ( wn wa wb con3 jca pm5.21 syl ) BFZAFZCFZGACHMNOABDICBEIJACKL $.
      $([16-Feb-96]$)
  $}


  ${
    pm5.21nii.1 $e |- ( ph -> ps ) $.
    pm5.21nii.2 $e |- ( ch -> ps ) $.
    pm5.21nii.3 $e |- ( ps -> ( ph <-> ch ) ) $.
    $( Elimination of antecedent implied by each side of biconditional. $)
    pm5.21nii   $p |- ( ph <-> ch ) $=
      ( wb pm5.21ni pm2.61i ) BACGFABCDEHI $.
      $([21-May-99]$)
  $}


  ${
    baib.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    $( Move conjunction outside of biconditional. $)
    baib   $p |- ( ps -> ( ph <-> ch ) ) $=
      ( wi wa biimp pm3.27d a1i biimpr exp impbid ) BACACEBABCABCFZDGHIBCAAMDJK
      L $.
      $([13-May-99]$)
  $}

  ${
    baibr.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    $( Move conjunction outside of biconditional. $)
    baibr   $p |- ( ps -> ( ch <-> ph ) ) $=
      ( wa biimpr exp wi biimp pm3.27d a1i impbid ) BCABCAABCEZDFGACHBABCAMDIJK
      L $.
      $([11-Jul-94]$)
  $}

  $( Move negation outside of biconditional.  Compare Theorem *5.18 of
     Whitehead and Russell. $)
  nbbn $p |- ( ( -. ph <-> ps ) <-> -. ( ph <-> ps ) ) $=
    ( wn wb wi wa pm2.61 pm2.65 pm2.03 syl5 im2and bi syl5ib annim syl6ib comm
    imnan sylib bineg sylibr wo pm2.21 nsyl4 adantl sylbir jca ax-1 adantr
    pm3.4 jaoi ianor 3imtr4 sylbi impbi ) ACZBDZABDZCZUPABEZBAEZFZCZURUPUSUTCZE
    VBUSUPVCUSUPBUOFZVCUSUOBEZBUOEZFZVDUPUSVEBVFUOABGUSABCZEUOVFABHBAIJKUOBLZMB
    ANZOPUSUTQRUQVAABLSZTURVBUPVKUSCZVCUAVGVBUPVLVGVCVLVEVFAVEUSUOABABUBZPVMUCV
    LAVHFVFABNVHVFABUOUBUDUEUFVCVEVFVCVDVEVJBVEUOBUOUGUHUEVCVDVFVJBUOUIUEUFUJUS
    UTUKVIULUMUN $.
    $([11-Jul-94]$)

  ${
    msca.1 $e |- ( ph -> ( ps -> ch ) ) $.
    msca.2 $e |- ( th -> ( ps -> -. ch ) ) $.
    $( Syllogism combined with contraposition. $)
    msca   $p |- ( ph -> ( ps -> -. th ) ) $=
      ( wn wa wi pm3.27 imp jc nsyl exp ) ABDGABHZBCGIDOBCABJABCEKLFMN $.
      $([5-Aug-93]$)
  $}

  $( Disjunction in consequent versus conjunction in antecedent.  Similar to
     Theorem *5.6 of Whitehead and Russell. $)
  orcana $p |- ( ( ph -> ( ps \/ ch ) ) <-> ( ( ph /\ -. ps ) -> ch ) ) $=
    ( wo wi wn wa df-or bilim impexp bitr4 ) ABCDZEABFZCEZEAMGCELNABCHIAMCJK $.
    $([8-Jun-94]$)

  ${
    orcanai.1 $e |- ( ph -> ( ps \/ ch ) ) $.
    $( Change disjunction in consequent to conjunction in antecedent. $)
    orcanai   $p |- ( ( ph /\ -. ps ) -> ch ) $=
      ( wn ord imp ) ABECABCDFG $.
      $([8-Jun-94]$)
  $}


  ${
    2th.1 $e |- ph $.
    2th.2 $e |- ps $.
    $( Two truths are equivalent. $)
    2th   $p |- ( ph <-> ps ) $=
      ( a1i impbi ) ABBADEABCEF $.
      $([18-Aug-93]$)
  $}

  ${
    intnan.1 $e |- -. ph $.
    $( Introduction of conjunct inside of a contradiction. $)
    intnan   $p |- -. ( ps /\ ph ) $=
      ( wa pm3.27 mto ) BADABAECF $.
      $([16-Sep-93]$)
  $}

  ${
    intnanr.1 $e |- -. ph $.
    $( Introduction of conjunct inside of a contradiction. $)
    intnanr   $p |- -. ( ph /\ ps ) $=
      ( wa pm3.26 mto ) ABDAABECF $.
      $([3-Apr-95]$)
  $}

  ${
    mpan.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    mpan.2 $e |- ph $.
    $( An inference based on modus ponens. $)
    mpan   $p |- ( ps -> ch ) $=
      ( wi exp ax-mp ) ABCFEABCDGH $.
      $([30-Aug-93]$)
  $}

  ${
    mpan2.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    mpan2.2 $e |- ps $.
    $( An inference based on modus ponens. $)
    mpan2   $p |- ( ph -> ch ) $=
      ( exp mpi ) ABCABCDFEG $.
      $([16-Sep-93]$)
  $}

  ${
    mp2an.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    mp2an.2 $e |- ph $.
    mp2an.3 $e |- ps $.
    $( An inference based on modus ponens. $)
    mp2an   $p |- ch $=
      ( mpan ax-mp ) BCFABCDEGH $.
      $([13-Apr-95]$)
  $}

  ${
    mpian.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    mpian.2 $e |- ( ph -> ps ) $.
    $( An inference based on modus ponens. $)
    mpian   $p |- ( ph -> ch ) $=
      ( exp mpd ) ABCABCDFEG $.
      $([23-May-99]$)
  $}

  ${
    mpani.1 $e |- ( ph -> ( ( ps /\ ch ) -> th ) ) $.
    mpani.2 $e |- ps $.
    $( An inference based on modus ponens. $)
    mpani   $p |- ( ph -> ( ch -> th ) ) $=
      ( wi exp3a mpi ) ABCDGABCDEHFI $.
      $([10-Apr-94]$)
  $}

  ${
    mpan2i.1 $e |- ( ph -> ( ( ps /\ ch ) -> th ) ) $.
    mpan2i.2 $e |- ch $.
    $( An inference based on modus ponens. $)
    mpan2i   $p |- ( ph -> ( ps -> th ) ) $=
      ( exp3a mpii ) ABCDABCDEGFH $.
      $([10-Apr-94]$)
  $}

  ${
    mpan11.1 $e |- ( ( ( ph /\ ps ) /\ ch ) -> th ) $.
    mpan11.2 $e |- ph $.
    $( An inference based on modus ponens. $)
    mpan11   $p |- ( ( ps /\ ch ) -> th ) $=
      ( wi wa exp mpan imp ) BCDABCDGABHCDEIFJK $.
      $([16-Aug-94]$)
  $}

  ${
    mpan12.1 $e |- ( ( ( ph /\ ps ) /\ ch ) -> th ) $.
    mpan12.2 $e |- ps $.
    $( An inference based on modus ponens. $)
    mpan12   $p |- ( ( ph /\ ch ) -> th ) $=
      ( wi wa exp mpan2 imp ) ACDABCDGABHCDEIFJK $.
      $([16-Aug-94]$)
  $}

  ${
    mpan21.1 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
    mpan21.2 $e |- ps $.
    $( An inference based on modus ponens. $)
    mpan21   $p |- ( ( ph /\ ch ) -> th ) $=
      ( wa exp mpani imp ) ACDABCDABCGDEHFIJ $.
      $([3-May-94]$)
  $}

  ${
    mpan22.1 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
    mpan22.2 $e |- ch $.
    $( An inference based on modus ponens. $)
    mpan22   $p |- ( ( ph /\ ps ) -> th ) $=
      ( wa exp mpan2i imp ) ABDABCDABCGDEHFIJ $.
      $([3-May-94]$)
  $}

  ${
    mp3an1.1 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    mp3an1.2 $e |- ph $.
    $( An inference based on modus ponens. $)
    mp3an1   $p |- ( ( ps /\ ch ) -> th ) $=
      ( wa 3expb mpan ) ABCGDABCDEHFI $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an2.1 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    mp3an2.2 $e |- ps $.
    $( An inference based on modus ponens. $)
    mp3an2   $p |- ( ( ph /\ ch ) -> th ) $=
      ( 3expa mpan12 ) ABCDABCDEGFH $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an3.1 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    mp3an3.2 $e |- ch $.
    $( An inference based on modus ponens. $)
    mp3an3   $p |- ( ( ph /\ ps ) -> th ) $=
      ( wa 3expa mpan2 ) ABGCDABCDEHFI $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an.1 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    mp3an.2 $e |- ph $.
    mp3an.3 $e |- ps $.
    mp3an.4 $e |- ch $.
    $( An inference based on modus ponens. $)
    mp3an   $p |- th $=
      ( mp3an1 mp2an ) BCDABCDEFIGHJ $.
      $([14-May-99]$)
  $}

  ${
    mt2bi.1 $e |- ph $.
    $( A false consequent falsifies an antecedent. $)
    mt2bi   $p |- ( -. ps <-> ( ps -> -. ph ) ) $=
      ( wn wi pm2.21 pm2.03 mpi impbi ) BDZBADZEZBKFLAJBAGCHI $.
      $([19-Aug-93]$)
  $}

  ${
    mtbid.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    mtbid.min $e |- ( ph -> -. ps ) $.
    $( A deduction from a biconditional, similar to modus tollens. $)
    mtbid     $p |- ( ph -> -. ch ) $=
      ( biimprd mtod ) ACBABCDFEG $.
      $([26-Nov-95]$)
  $}

  ${
    mtbird.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    mtbird.min $e |- ( ph -> -. ch ) $.
    $( A deduction from a biconditional, similar to modus tollens. $)
    mtbird     $p |- ( ph -> -. ps ) $=
      ( biimpd mtod ) ABCABCDFEG $.
      $([10-May-94]$)
  $}

  ${
    mtbii.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    mtbii.min $e |- -. ps $.
    $( An inference from a biconditional, similar to modus tollens. $)
    mtbii     $p |- ( ph -> -. ch ) $=
      ( biimprd mtoi ) ACBABCDFEG $.
      $([27-Nov-95]$)
  $}

  ${
    mtbiri.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    mtbiri.min $e |- -. ch $.
    $( An inference from a biconditional, similar to modus tollens. $)
    mtbiri     $p |- ( ph -> -. ps ) $=
      ( biimpd mtoi ) ABCABCDFEG $.
      $([24-Aug-95]$)
  $}

  ${
    tbt.1 $e |- ph $.
    $( A wff is equivalent to its equivalence with truth. $)
    tbt   $p |- ( ps <-> ( ps <-> ph ) ) $=
      ( wb a1i a1d ax-1 impbid bi2 mpi impbi ) BBADZBBABABABCEFBAGHLABBAICJK $.
      $([18-Aug-93]$)
  $}

  ${
    nbn.1 $e |- -. ph $.
    $( The negation of a wff is equivalent to the wff's equivalence to
       falsehood. $)
    nbn   $p |- ( -. ps <-> ( ps <-> ph ) ) $=
      ( wn wb pm2.21 a1i pm2.21d impbid bi1 mtoi impbi ) BDZBAEZMBABAFMABADMCGH
      INBABAJCKL $.
      $([5-Aug-93]$)
  $}

  ${
    biantru.1 $e |- ph $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantru   $p |- ( ps <-> ( ps /\ ph ) ) $=
      ( wa jctr pm3.26 impbi ) BBADBACEBAFG $.
      $([5-Aug-93]$)
  $}

  ${
    biantrur.1 $e |- ph $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrur   $p |- ( ps <-> ( ph /\ ps ) ) $=
      ( wa biantru ancom bitr ) BBADABDABCEBAFG $.
      $([3-Aug-94]$)
  $}

  ${
    biantrud.1 $e |- ( ph -> ps ) $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrud   $p |- ( ph -> ( ch <-> ( ch /\ ps ) ) ) $=
      ( wa imlan exp comm wi pm3.26 a1i impbid ) ACCBEZCAMCAMABCDFGHMCIACBJKL
      $.
      $([2-Aug-94]$)
  $}

  ${
    biantrurd.1 $e |- ( ph -> ps ) $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrurd   $p |- ( ph -> ( ch <-> ( ps /\ ch ) ) ) $=
      ( wa biantrud ancom syl6bb ) ACCBEBCEABCDFCBGH $.
      $([1-May-95]$)
  $}

  ${
    mpbiran.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    mpbiran.2 $e |- ps $.
    $( Detach truth from conjunction in biconditional. $)
    mpbiran   $p |- ( ph <-> ch ) $=
      ( wa biantrur bitr4 ) ABCFCDBCEGH $.
      $([27-Feb-96]$)
  $}

  ${
    mpbiranr.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    mpbiranr.2 $e |- ch $.
    $( Detach truth from conjunction in biconditional. $)
    mpbiranr   $p |- ( ph <-> ps ) $=
      ( wa biantru bitr4 ) ABCFBDCBEGH $.
      $([22-Feb-96]$)
  $}

  $( A wff is equivalent to itself with true antecedent. $)
  biimt $p |- ( ph -> ( ps <-> ( ph -> ps ) ) ) $=
    ( wi ax-1 a1i pm2.27 impbid ) ABABCZBHCABADEABFG $.
    $([28-Jan-96]$)

  $( A wff is disjoined with truth is true. $)
  biort $p |- ( ph -> ( ph <-> ( ph \/ ps ) ) ) $=
    ( wo orc a1d ax-1 impbid ) AAABCZAHAABDEAHFG $.
    $([23-May-99]$)

  $( A wff is equivalent to its disjunction with falsehood. $)
  biorf $p |- ( -. ph -> ( ps <-> ( ph \/ ps ) ) ) $=
    ( wn wi wo biimt df-or syl6bbr ) ACZBIBDABEIBFABGH $.
    $([23-Mar-95]$)

  ${
    biorfi.1 $e |- -. ph $.
    $( A wff is equivalent to its disjunction with falsehood. $)
    biorfi   $p |- ( ps <-> ( ps \/ ph ) ) $=
      ( wo wn wb biorf ax-mp orcom bitr ) BABDZBADAEBKFCABGHABIJ $.
      $([23-Mar-95]$)
  $}

  ${
    bianfi.1 $e |- -. ph $.
    $( A wff conjoined with falsehood is false. $)
    bianfi   $p |- ( ph <-> ( ps /\ ph ) ) $=
      ( wa pm2.21i pm3.27 impbi ) ABADZAHCEBAFG $.
      $([5-Aug-93]$)
  $}

  ${
    bianfd.1 $e |- ( ph -> -. ps ) $.
    $( A wff conjoined with falsehood is false. $)
    bianfd   $p |- ( ph -> ( ps <-> ( ps /\ ch ) ) ) $=
      ( wa pm2.21d wi pm3.26 a1i impbid ) ABBCEZABKDFKBGABCHIJ $.
      $([27-Mar-95]$)
  $}

  $( Negation inferred from embedded conjunct. $)
  pclem6 $p |- ( ( ph <-> ( ps /\ -. ph ) ) -> -. ps ) $=
    ( wn wa wb wi bi2 exp3a com23 pm2.16 syl6 pm2.43d bi1 pm3.27 pm2.01d mpd )
    ABACZDZEZQBCZSQTSQBAFQTFSBQASBQAARGHIBAJKLSASARQARMBQNKOP $.
    $([20-Aug-93]$)

  $( A transitive law of equivalence.  Compare Theorem *4.22 of Whitehead
     and Russell. $)
  biantr $p |- ( ( ( ph <-> ps ) /\ ( ch <-> ps ) ) -> ( ph <-> ch ) ) $=
    ( wb pm4.21 biimp birbid syl5bi imp ) ABDZCBDZACDZJBCDZLKJBACJBADABEFGKMCBE
    FHI $.
    $([18-Aug-93]$)

  $( Removal of conjunct from one side of an equivalence. $)
  bimsc1 $p |- ( ( ( ph -> ps ) /\ ( ch <-> ( ps /\ ph ) ) )
               -> ( ch <-> ph ) ) $=
    ( wi wa wb pm3.27 syl3 ax-2 comm impexp biimp com23 syl5 imp im2an an1 bi
    3imtr4 sylan5b ) ABDZCBAEZDZUBCDZEZCAFZCUBFUCUAUDEZECADZACDZEUAUEEUFUCUHUGU
    IUBACBAGHUAUDUIUAABCDDZUIUDUJUAUIABCIJUDBACUDBUIDBACKLMNOPUAUCUDQCARSCUBRT
    $.
    $([5-Aug-93]$)

  ${
    ecased.1 $e |- ( ph -> ( ps \/ ch \/ th ) ) $.
    ecased.2 $e |- ( ph -> -. ch ) $.
    ecased.3 $e |- ( ph -> -. th ) $.
    $( Deduction for elimination by cases. $)
    ecased   $p |- ( ph -> ps ) $=
      ( wo w3o 3orass sylib ord wn wa jca ioran sylibr mt3d ) ABCDHZABSABCDIBSH
      EBCDJKLACMZDMZNSMATUAFGOCDPQR $.
      $([22-Apr-94]$)
  $}

  ${
    ecase2d.1 $e |- ( ph -> ps ) $.
    ecase2d.2 $e |- ( ph -> -. ( ps /\ ch ) ) $.
    ecase2d.3 $e |- ( ph -> -. ( ps /\ th ) ) $.
    ecase2d.4 $e |- ( ph -> ( ta \/ ( ch \/ th ) ) ) $.
    $( Deduction for elimination by cases. $)
    ecase2d   $p |- ( ph -> ta ) $=
      ( wo wn orcom sylib ord wa wi imnan sylibr mpd jca ioran ) ACDJZKZEAUBEAE
      UBJUBEJIEUBLMNACKZDKZOUCAUDUEABUDABCOKBUDPGBCQRFSABUEABDOKBUEPHBDQRFSTCDU
      ARS $.
      $([21-Apr-94]$)
  $}

  ${
    ecase3.1 $e |- ( ph -> ch ) $.
    ecase3.2 $e |- ( ps -> ch ) $.
    ecase3.3 $e |- ( -. ( ph \/ ps ) -> ch ) $.
    $( Inference for elimination by cases. $)
    ecase3   $p |- ch $=
      ( wn wa wo ioran sylbir exp pm2.61ii ) ABCAGZBGZCNOHABIGCABJFKLDEM $.
      $([23-Mar-95]$)
  $}

  ${
    ecase3d.1 $e |- ( ph -> ( ps -> th ) ) $.
    ecase3d.2 $e |- ( ph -> ( ch -> th ) ) $.
    ecase3d.3 $e |- ( ph -> ( -. ( ps \/ ch ) -> th ) ) $.
    $( Deduction for elimination by cases. $)
    ecase3d   $p |- ( ph -> th ) $=
      ( wi comm wo wn ecase3 ) BCADHABDEIACDFIABCJKDGIL $.
      $([2-May-96]$)
  $}

  $( Lemma for combining cases. $)
  caselem $p |- ( ( ( ph \/ ps ) /\ ( ch \/ th ) ) <->
  ( ( ( ph /\ ch ) \/ ( ps /\ ch ) ) \/ ( ( ph /\ th ) \/ ( ps /\ th ) ) ) ) $=
    ( wo wa andi andir bi2or bitr ) ABEZCDEFKCFZKDFZEACFBCFEZADFBDFEZEKCDGLNMOA
    BCHABDHIJ $.
    $([29-Jul-99]$)

  ${
    ccase.1 $e |- ( ( ph /\ ps ) -> ta ) $.
    ccase.2 $e |- ( ( ch /\ ps ) -> ta ) $.
    ccase.3 $e |- ( ( ph /\ th ) -> ta ) $.
    ccase.4 $e |- ( ( ch /\ th ) -> ta ) $.
    $( Inference for combining cases. $)
    ccase $p |- ( ( ( ph \/ ch ) /\ ( ps \/ th ) ) -> ta ) $=
      ( wo wa caselem jaoi sylbi ) ACJBDJKABKZCBKZJZADKZCDKZJZJEACBDLQETOEPFGMR
      ESHIMMN $.
      $([29-Jul-99]$)
  $}

  ${
    ccase2.1 $e |- ( ( ph /\ ps ) -> ta ) $.
    ccase2.2 $e |- ( ch -> ta ) $.
    ccase2.3 $e |- ( th -> ta ) $.
    $( Inference for combining cases. $)
    ccase2 $p |- ( ( ( ph \/ ch ) /\ ( ps \/ th ) ) -> ta ) $=
      ( adantr adantl ccase ) ABCDEFCEBGIDEAHJDECHJK $.
      $([29-Jul-99]$)
  $}

  ${
    niabn.1 $e |- ph $.
    $( Miscellaneous inference relating falsehoods. $)
    niabn   $p |- ( -. ps -> ( ( ch /\ ps ) <-> -. ph ) ) $=
      ( wn wa pm2.21 adantld wi mpi a1i impbid ) BEZCBFZAEZMBOCBOGHONIMOANANGDJ
      KL $.
      $([31-Mar-94]$)
  $}

  $( Miscellaneous introduction of complexity into an equivalence. $)
  ibiia $p |- ( ph -> ( ps <-> ( ( ch -> ph ) -> ( ps /\ ph ) ) ) ) $=
    ( wi wa ax-1 a1i syl4 comm impbid iba bilimd bitrd ) ABCADZBDZNBAEZDABOBODA
    BNFGOABANBACFHIJABPNABKLM $.
    $([2-Apr-94]$)

  $( Miscellaneous introduction of complexity into an equivalence. $)
  nibiia $p |- ( -. ph -> ( ps <-> ( ( ps -> ph ) -> ( ch /\ ph ) ) ) ) $=
    ( wn wi wa pm2.21 syl3d com23 pm3.27 syl34 pm2.15d comm impbid ) ADZBBAEZCA
    FZEZOPBQOAQBAQGHIROBRBABDPQABAGCAJKLMN $.
    $([2-Apr-94]$)

  $( Miscellaneous introduction of complexity into an equivalence. $)
  nibaoa $p |- ( -. ph -> ( ps <-> ( ( ch /\ ph ) \/ ( ps /\ -. ph ) ) ) ) $=
    ( wn wa wo pm3.21 olc syl6 wi pm2.21 com23 imp3a pm3.26 a1i jaod impbid )
    ADZBCAEZBREZFZRBTUARBGTSHIRSBTRCABRACBACBJKLMTBJRBRNOPQ $.
    $([15-May-99]$)

  ${
    ninba.1 $e |- ph $.
    $( Miscellaneous inference relating falsehoods. $)
    ninba   $p |- ( -. ps -> ( -. ph <-> ( ch /\ ps ) ) ) $=
      ( wn wa wi pm2.21 mpi a1i adantld impbid ) BEZAEZCBFZNOGMNAOAOHDIJMBNCBNH
      KL $.
      $([31-Mar-94]$)
  $}

  ${
    $v et $. $( Greek eta $)
    wet $f wff et $.
    prlem1.1 $e |- ( ph -> ( et <-> ch ) ) $.
    prlem1.2 $e |- ( ps -> -. th ) $.
    $( A specialized lemma for set theory (axiom of pairing). $)
    prlem1   $p |- ( ph -> ( ps ->
                  ( ( ( ps /\ ch ) \/ ( th /\ ta ) ) -> et ) ) ) $=
      ( wa wo wi biimprcd adantl a1d com23 wn pm2.21 comm syl5 adantr jaoi
      com3l ) BCIZDEIZJABFUCABFKZKUDUCBAFUCAFKZBCUFBAFCGLMNOUDUEADUEEDDPZFBUGDF
      DFQRHSTNUAUB $.
      $([18-Oct-95]$)
  $}

  $( A specialized lemma for set theory (axiom of pairing). $)
  prlem2 $p |- ( ( ( ph /\ ps ) \/ ( ch /\ th ) ) <->
               ( ( ph \/ ch ) /\ ( ( ph /\ ps ) \/ ( ch /\ th ) ) ) ) $=
    ( wa wo oel biran anass bitr orcom bi2or andi bitr4 ) ABEZCDEZFZACFZOEZRPEZ
    FRQEOSPTORAEZBESAUABACGHRABIJPRCEZDETCUBDCCAFZCEUBCAGUCRCCAKHJHRCDIJLROPMN
    $.
    $([5-Aug-93]$)

  ${
    prlem3.1 $e |- ph $.
    prlem3.2 $e |- ps $.
    $( A specialized lemma for set theory (axiom of pairing) $)
    prlem3   $p |- ( ( ch \/ th ) <-> ( ( ph /\ ch ) \/ ( ps /\ th ) ) ) $=
      ( wa biantrur bi2or ) CACGDBDGACEHBDFHI $.
      $([5-Aug-93]$)
  $}

  ${
    oplem1.1 $e |- ( ph -> ( ps \/ ch ) ) $.
    oplem1.2 $e |- ( ph -> ( th \/ ta ) ) $.
    oplem1.3 $e |- ( ps <-> th ) $.
    oplem1.4 $e |- ( ch -> ( th <-> ta ) ) $.
    $( A specialized lemma for set theory (ordered pair theorem). $)
    oplem1   $p |- ( ph -> ps ) $=
      ( wn wi wa ord bineg syl5ib jcad syl5bb biimpar syl6 pm2.18 syl ) ABJZBKB
      AUBCELBAUBCEABCFMADJEUBADEGMBDHNOPCBECDEBIHQRSBTUA $.
      $([18-Oct-95]$)
  $}

  $( Lemma used in construction of real numbers. $)
  rnlem $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
  ( ( ( ph /\ ch ) /\ ( ps /\ th ) ) /\ ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) ) $=
    ( wa anandir anandi bi2an ancom bilan an4 bitr 3bitr ) ABECDEZEANEZBNEZEACE
    ZADEZEZBCEZBDEZEZEZQUAERTEEZABNFOSPUBACDGBCDGHUCSUATEZEUDUBUESTUAIJQRUATKLM
    $.
    $([4-Sep-95]$)

