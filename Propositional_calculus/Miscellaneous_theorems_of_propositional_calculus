$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Miscellaneous theorems of propositional calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two propositions are equivalent if they are both true.  Theorem *5.1 of
     [WhiteheadRussell] p. 123. $)
  pm5.1 $p |- ( ( ph /\ ps ) -> ( ph <-> ps ) ) $=
    ( wa pm3.4 pm3.26 a1d impbid ) ABCZABABDHABABEFG $.
    $([21-May-94]$)

  $( Two propositions are equivalent if they are both false.  Theorem *5.21 of
     [WhiteheadRussell] p. 124. $)
  pm5.21 $p |- ( ( -. ph /\ -. ps ) -> ( ph <-> ps ) ) $=
    ( wn wa pm5.1 bicon4d ) ACZBCZDABGHEF $.
    $([21-May-94]$)


  ${
    pm5.21ni.1 $e |- ( ph -> ps ) $.
    pm5.21ni.2 $e |- ( ch -> ps ) $.
    $( Two propositions implying a false one are equivalent. $)
    pm5.21ni   $p |- ( -. ps -> ( ph <-> ch ) ) $=
      ( wn wb pm5.21 con3 sylan13 ) AFCFACGBFACHABDICBEIJ $.
      $([16-Feb-96]$)
  $}


  ${
    pm5.21nii.1 $e |- ( ph -> ps ) $.
    pm5.21nii.2 $e |- ( ch -> ps ) $.
    pm5.21nii.3 $e |- ( ps -> ( ph <-> ch ) ) $.
    $( Elimination of antecedent implied by each side of biconditional. $)
    pm5.21nii   $p |- ( ph <-> ch ) $=
      ( wb pm5.21ni pm2.61i ) BACGFABCDEHI $.
      $([21-May-99]$)
  $}

  $( Elimination of antecedents in an implication. $)
  elimant $p |- ( ( ( ph -> ps ) /\ ( ( ps -> ch ) -> ( ph -> th ) ) ) ->
                ( ph -> ( ch -> th ) ) ) $=
    ( wi wa wb id impac pm5.1 syl birimd biimpd exp com23 imp pm5.41 syl6ibr
    pm2.43d ) ABEZBCEZADEZEZFZACDEZUDAACEZUBEZAUEETUCAUGETAUCUGTAUCUGETAFZUCUGU
    HUAUFUBUHBACUHBAFBAGTABTHIBAJKLLMNOPACDQRS $.
    $([13-Oct-99]$)

  ${
    baib.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    $( Move conjunction outside of biconditional. $)
    baib   $p |- ( ps -> ( ph <-> ch ) ) $=
      ( wi pm3.27bd a1i wa biimpr exp impbid ) BACACEBABCDFGBCAABCHDIJK $.
      $([13-May-99]$)
  $}

  ${
    baibr.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    $( Move conjunction outside of biconditional. $)
    baibr   $p |- ( ps -> ( ch <-> ph ) ) $=
      ( wa biimpr exp wi pm3.27bd a1i impbid ) BCABCAABCEDFGACHBABCDIJK $.
      $([11-Jul-94]$)
  $}

  ${
    msca.1 $e |- ( ph -> ( ps -> ch ) ) $.
    msca.2 $e |- ( th -> ( ps -> -. ch ) ) $.
    $( Syllogism combined with contraposition. $)
    msca   $p |- ( ph -> ( ps -> -. th ) ) $=
      ( wn wa wi pm3.27 imp jc nsyl exp ) ABDGABHZBCGIDOBCABJABCEKLFMN $.
      $([5-Aug-93]$)
  $}

  $( Disjunction in consequent versus conjunction in antecedent.  Similar to
     Theorem *5.6 of [WhiteheadRussell] p. 125. $)
  orcana $p |- ( ( ph -> ( ps \/ ch ) ) <-> ( ( ph /\ -. ps ) -> ch ) ) $=
    ( wo wi wn wa df-or bilim impexp bitr4 ) ABCDZEABFZCEZEAMGCELNABCHIAMCJK $.
    $([8-Jun-94]$)

  $( Theorem to move a conjunct in and out of a negation. $)
  nan $p |- ( ( ph -> -. ( ps /\ ch ) ) <-> ( ( ph /\ ps ) -> -. ch ) ) $=
    ( wa wn wi impexp imnan bilim bitr2 ) ABDCEZFABKFZFABCDEZFABKGLMABCHIJ $.
    $([9-Nov-03]$)

  ${
    orcanai.1 $e |- ( ph -> ( ps \/ ch ) ) $.
    $( Change disjunction in consequent to conjunction in antecedent. $)
    orcanai   $p |- ( ( ph /\ -. ps ) -> ch ) $=
      ( wn ord imp ) ABECABCDFG $.
      $([8-Jun-94]$)
  $}


  ${
    intnan.1 $e |- -. ph $.
    $( Introduction of conjunct inside of a contradiction. $)
    intnan   $p |- -. ( ps /\ ph ) $=
      ( wa pm3.27 mto ) BADACBAEF $.
      $([16-Sep-93]$)
  $}

  ${
    intnanr.1 $e |- -. ph $.
    $( Introduction of conjunct inside of a contradiction. $)
    intnanr   $p |- -. ( ph /\ ps ) $=
      ( wa pm3.26 mto ) ABDACABEF $.
      $([3-Apr-95]$)
  $}

  ${
    mpan.1 $e |- ph $.
    mpan.2 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( An inference based on modus ponens. $)
    mpan   $p |- ( ps -> ch ) $=
      ( wi exp ax-mp ) ABCFDABCEGH $.
      $([30-Aug-93]$)
  $}

  ${
    mpan2.1 $e |- ps $.
    mpan2.2 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( An inference based on modus ponens. $)
    mpan2   $p |- ( ph -> ch ) $=
      ( exp mpi ) ABCDABCEFG $.
      $([16-Sep-93]$)
  $}

  ${
    mp2an.1 $e |- ph $.
    mp2an.2 $e |- ps $.
    mp2an.3 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( An inference based on modus ponens. $)
    mp2an   $p |- ch $=
      ( mpan ax-mp ) BCEABCDFGH $.
      $([13-Apr-95]$)
  $}

  ${
    mpian.1 $e |- ( ph -> ps ) $.
    mpian.2 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( An inference based on modus ponens. $)
    mpian   $p |- ( ph -> ch ) $=
      ( exp mpd ) ABCDABCEFG $.
      $([23-May-99]$)
  $}

  ${
    mpani.1 $e |- ps $.
    mpani.2 $e |- ( ph -> ( ( ps /\ ch ) -> th ) ) $.
    $( An inference based on modus ponens. $)
    mpani   $p |- ( ph -> ( ch -> th ) ) $=
      ( wi exp3a mpi ) ABCDGEABCDFHI $.
      $([10-Apr-94]$)
  $}

  ${
    mpan2i.1 $e |- ch $.
    mpan2i.2 $e |- ( ph -> ( ( ps /\ ch ) -> th ) ) $.
    $( An inference based on modus ponens. $)
    mpan2i   $p |- ( ph -> ( ps -> th ) ) $=
      ( exp3a mpii ) ABCDEABCDFGH $.
      $([10-Apr-94]$)
  $}

  ${
    mpan11.1 $e |- ph $.
    mpan11.2 $e |- ( ( ( ph /\ ps ) /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mpan11   $p |- ( ( ps /\ ch ) -> th ) $=
      ( wi wa exp mpan imp ) BCDABCDGEABHCDFIJK $.
      $([16-Aug-94]$)
  $}

  ${
    mpan12.1 $e |- ps $.
    mpan12.2 $e |- ( ( ( ph /\ ps ) /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mpan12   $p |- ( ( ph /\ ch ) -> th ) $=
      ( wi wa exp mpan2 imp ) ACDABCDGEABHCDFIJK $.
      $([16-Aug-94]$)
  $}

  ${
    mpan21.1 $e |- ps $.
    mpan21.2 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
    $( An inference based on modus ponens. $)
    mpan21   $p |- ( ( ph /\ ch ) -> th ) $=
      ( wa exp mpani imp ) ACDABCDEABCGDFHIJ $.
      $([3-May-94]$)
  $}

  ${
    mpan22.1 $e |- ch $.
    mpan22.2 $e |- ( ( ph /\ ( ps /\ ch ) ) -> th ) $.
    $( An inference based on modus ponens. $)
    mpan22   $p |- ( ( ph /\ ps ) -> th ) $=
      ( wa exp mpan2i imp ) ABDABCDEABCGDFHIJ $.
      $([3-May-94]$)
  $}

  ${
    mp3an1.1 $e |- ph $.
    mp3an1.2 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mp3an1   $p |- ( ( ps /\ ch ) -> th ) $=
      ( wa 3expb mpan ) ABCGDEABCDFHI $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an2.1 $e |- ps $.
    mp3an2.2 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mp3an2   $p |- ( ( ph /\ ch ) -> th ) $=
      ( 3expa mpan12 ) ABCDEABCDFGH $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an3.1 $e |- ch $.
    mp3an3.2 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mp3an3   $p |- ( ( ph /\ ps ) -> th ) $=
      ( wa 3expa mpan2 ) ABGCDEABCDFHI $.
      $([21-Nov-94]$)
  $}

  ${
    mp3an.1 $e |- ph $.
    mp3an.2 $e |- ps $.
    mp3an.3 $e |- ch $.
    mp3an.4 $e |- ( ( ph /\ ps /\ ch ) -> th ) $.
    $( An inference based on modus ponens. $)
    mp3an   $p |- th $=
      ( mp3an1 mp2an ) BCDFGABCDEHIJ $.
      $([14-May-99]$)
  $}

  $( Modus-tollens-like theorem. $)
  mtt $p |- ( -. ph -> ( -. ps <-> ( ps -> ph ) ) ) $=
    ( wn wi pm2.21 a1i pm2.16 comm impbid ) ACZBCZBADZKLDJBAEFLJKBAGHI $.
    $([7-Apr-01]$)

  ${
    mt2bi.1 $e |- ph $.
    $( A false consequent falsifies an antecedent. $)
    mt2bi   $p |- ( -. ps <-> ( ps -> -. ph ) ) $=
      ( wn wi pm2.21 pm2.03 mpi impbi ) BDZBADZEZBKFLAJCBAGHI $.
      $([19-Aug-93]$)
  $}

  ${
    mtbid.min $e |- ( ph -> -. ps ) $.
    mtbid.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( A deduction from a biconditional, similar to modus tollens. $)
    mtbid     $p |- ( ph -> -. ch ) $=
      ( biimprd mtod ) ACBDABCEFG $.
      $([26-Nov-95]$)
  $}

  ${
    mtbird.min $e |- ( ph -> -. ch ) $.
    mtbird.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( A deduction from a biconditional, similar to modus tollens. $)
    mtbird     $p |- ( ph -> -. ps ) $=
      ( biimpd mtod ) ABCDABCEFG $.
      $([10-May-94]$)
  $}

  ${
    mtbii.min $e |- -. ps $.
    mtbii.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( An inference from a biconditional, similar to modus tollens. $)
    mtbii     $p |- ( ph -> -. ch ) $=
      ( biimprd mtoi ) ACBDABCEFG $.
      $([27-Nov-95]$)
  $}

  ${
    mtbiri.min $e |- -. ch $.
    mtbiri.maj $e |- ( ph -> ( ps <-> ch ) ) $.
    $( An inference from a biconditional, similar to modus tollens. $)
    mtbiri     $p |- ( ph -> -. ps ) $=
      ( biimpd mtoi ) ABCDABCEFG $.
      $([24-Aug-95]$)
  $}

  ${
    2th.1 $e |- ph $.
    2th.2 $e |- ps $.
    $( Two truths are equivalent. $)
    2th   $p |- ( ph <-> ps ) $=
      ( a1i impbi ) ABBADEABCEF $.
      $([18-Aug-93]$)
  $}

  ${
    tbt.1 $e |- ph $.
    $( A wff is equivalent to its equivalence with truth. $)
    tbt   $p |- ( ps <-> ( ps <-> ph ) ) $=
      ( wb a1i a1d ax-1 impbid bi2 mpi impbi ) BBADZBBABABABCEFBAGHLABCBAIJK $.
      $([18-Aug-93]$)
  $}

  ${
    nbn.1 $e |- -. ph $.
    $( The negation of a wff is equivalent to the wff's equivalence to
       falsehood. $)
    nbn   $p |- ( -. ps <-> ( ps <-> ph ) ) $=
      ( wn wb pm2.21 a1i pm2.21d impbid bi1 mtoi impbi ) BDZBAEZMBABAFMABADMCGH
      INBACBAJKL $.
      $([5-Aug-93]$)
  $}

  ${
    biantru.1 $e |- ph $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantru   $p |- ( ps <-> ( ps /\ ph ) ) $=
      ( wa jctr pm3.26 impbi ) BBADBACEBAFG $.
      $([5-Aug-93]$)
  $}

  ${
    biantrur.1 $e |- ph $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrur   $p |- ( ps <-> ( ph /\ ps ) ) $=
      ( wa biantru ancom bitr ) BBADABDABCEBAFG $.
      $([3-Aug-94]$)
  $}

  ${
    biantrud.1 $e |- ( ph -> ps ) $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrud   $p |- ( ph -> ( ch <-> ( ch /\ ps ) ) ) $=
      ( wa imlan exp comm wi pm3.26 a1i impbid ) ACCBEZCAMCAMABCDFGHMCIACBJKL
      $.
      $([2-Aug-94]$)
  $}

  ${
    biantrurd.1 $e |- ( ph -> ps ) $.
    $( A wff is equivalent to its conjunction with truth. $)
    biantrurd   $p |- ( ph -> ( ch <-> ( ps /\ ch ) ) ) $=
      ( wa biantrud ancom syl6bb ) ACCBEBCEABCDFCBGH $.
      $([1-May-95]$)
  $}

  ${
    mpbiran.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    mpbiran.2 $e |- ps $.
    $( Detach truth from conjunction in biconditional. $)
    mpbiran   $p |- ( ph <-> ch ) $=
      ( wa biantrur bitr4 ) ABCFCDBCEGH $.
      $([27-Feb-96]$)
  $}

  ${
    mpbiranr.1 $e |- ( ph <-> ( ps /\ ch ) ) $.
    mpbiranr.2 $e |- ch $.
    $( Detach truth from conjunction in biconditional. $)
    mpbiranr   $p |- ( ph <-> ps ) $=
      ( wa biantru bitr4 ) ABCFBDCBEGH $.
      $([22-Feb-96]$)
  $}

  $( A wff is equivalent to itself with true antecedent. $)
  biimt $p |- ( ph -> ( ps <-> ( ph -> ps ) ) ) $=
    ( wi ax-1 a1i pm2.27 impbid ) ABABCZBHCABADEABFG $.
    $([28-Jan-96]$)

  $( A wff is disjoined with truth is true. $)
  biort $p |- ( ph -> ( ph <-> ( ph \/ ps ) ) ) $=
    ( wo orc a1d ax-1 impbid ) AAABCZAHAABDEAHFG $.
    $([23-May-99]$)

  $( A wff is equivalent to its disjunction with falsehood. $)
  biorf $p |- ( -. ph -> ( ps <-> ( ph \/ ps ) ) ) $=
    ( wn wi wo biimt df-or syl6bbr ) ACZBIBDABEIBFABGH $.
    $([23-Mar-95]$)

  ${
    biorfi.1 $e |- -. ph $.
    $( A wff is equivalent to its disjunction with falsehood. $)
    biorfi   $p |- ( ps <-> ( ps \/ ph ) ) $=
      ( wo wn wb biorf ax-mp orcom bitr ) BABDZBADAEBKFCABGHABIJ $.
      $([23-Mar-95]$)
  $}

  ${
    bianfi.1 $e |- -. ph $.
    $( A wff conjoined with falsehood is false. $)
    bianfi   $p |- ( ph <-> ( ps /\ ph ) ) $=
      ( wa pm2.21i pm3.27 impbi ) ABADZAHCEBAFG $.
      $([5-Aug-93]$)
  $}

  ${
    bianfd.1 $e |- ( ph -> -. ps ) $.
    $( A wff conjoined with falsehood is false. $)
    bianfd   $p |- ( ph -> ( ps <-> ( ps /\ ch ) ) ) $=
      ( wa pm2.21d wi pm3.26 a1i impbid ) ABBCEZABKDFKBGABCHIJ $.
      $([27-Mar-95]$)
  $}

  $( Negation inferred from embedded conjunct. $)
  pclem6 $p |- ( ( ph <-> ( ps /\ -. ph ) ) -> -. ps ) $=
    ( wn wa wb bi1 pm3.27 syl6 pm2.01d wi bi2 exp3a com23 pm2.16 pm2.43d mpd )
    ABACZDZEZQBCZSASARQARFBQGHISQTSQBAJQTJSBQASBQAARKLMBANHOP $.
    $([20-Aug-93]$)

  $( A transitive law of equivalence.  Compare Theorem *4.22 of
     [WhiteheadRussell] p. 117. $)
  biantr $p |- ( ( ( ph <-> ps ) /\ ( ch <-> ps ) ) -> ( ph <-> ch ) ) $=
    ( wb pm4.21 biimp birbid syl5bi imp ) ABDZCBDZACDZJBCDZLKJBACJBADABEFGKMCBE
    FHI $.
    $([18-Aug-93]$)

  $( Removal of conjunct from one side of an equivalence. $)
  bimsc1 $p |- ( ( ( ph -> ps ) /\ ( ch <-> ( ps /\ ph ) ) )
               -> ( ch <-> ph ) ) $=
    ( wa wb wi id pm4.71r biimp bicomd sylan9bbr ) CBADZEZCLABFZAMGNALNALEABHIJ
    K $.
    $([5-Aug-93]$)

  ${
    ecased.1 $e |- ( ph -> ( ps \/ ch \/ th ) ) $.
    ecased.2 $e |- ( ph -> -. ch ) $.
    ecased.3 $e |- ( ph -> -. th ) $.
    $( Deduction for elimination by cases. $)
    ecased   $p |- ( ph -> ps ) $=
      ( wo wn wa jca ioran sylibr w3o 3orass sylib ord mt3d ) ABCDHZACIZDIZJSIA
      TUAFGKCDLMABSABCDNBSHEBCDOPQR $.
      $([22-Apr-94]$)
  $}

  ${
    ecase2d.1 $e |- ( ph -> ps ) $.
    ecase2d.2 $e |- ( ph -> -. ( ps /\ ch ) ) $.
    ecase2d.3 $e |- ( ph -> -. ( ps /\ th ) ) $.
    ecase2d.4 $e |- ( ph -> ( ta \/ ( ch \/ th ) ) ) $.
    $( Deduction for elimination by cases. $)
    ecase2d   $p |- ( ph -> ta ) $=
      ( wo wn wa wi imnan sylibr mpd jca ioran orcom sylib ord ) ACDJZKZEACKZDK
      ZLUCAUDUEABUDFABCLKBUDMGBCNOPABUEFABDLKBUEMHBDNOPQCDROAUBEAEUBJUBEJIEUBST
      UAP $.
      $([21-Apr-94]$)
  $}

  ${
    ecase3.1 $e |- ( ph -> ch ) $.
    ecase3.2 $e |- ( ps -> ch ) $.
    ecase3.3 $e |- ( -. ( ph \/ ps ) -> ch ) $.
    $( Inference for elimination by cases. $)
    ecase3   $p |- ch $=
      ( wn wa wo ioran sylbir exp pm2.61ii ) ABCAGZBGZCNOHABIGCABJFKLDEM $.
      $([23-Mar-95]$)
  $}

  ${
    ecase3d.1 $e |- ( ph -> ( ps -> th ) ) $.
    ecase3d.2 $e |- ( ph -> ( ch -> th ) ) $.
    ecase3d.3 $e |- ( ph -> ( -. ( ps \/ ch ) -> th ) ) $.
    $( Deduction for elimination by cases. $)
    ecase3d   $p |- ( ph -> th ) $=
      ( wi comm wo wn ecase3 ) BCADHABDEIACDFIABCJKDGIL $.
      $([2-May-96]$)
  $}

  $( Lemma for combining cases. $)
  caselem $p |- ( ( ( ph \/ ps ) /\ ( ch \/ th ) ) <->
  ( ( ( ph /\ ch ) \/ ( ps /\ ch ) ) \/ ( ( ph /\ th ) \/ ( ps /\ th ) ) ) ) $=
    ( wo wa andi andir bi2or bitr ) ABEZCDEFKCFZKDFZEACFBCFEZADFBDFEZEKCDGLNMOA
    BCHABDHIJ $.
    $([29-Jul-99]$)

  ${
    ccase.1 $e |- ( ( ph /\ ps ) -> ta ) $.
    ccase.2 $e |- ( ( ch /\ ps ) -> ta ) $.
    ccase.3 $e |- ( ( ph /\ th ) -> ta ) $.
    ccase.4 $e |- ( ( ch /\ th ) -> ta ) $.
    $( Inference for combining cases. $)
    ccase $p |- ( ( ( ph \/ ch ) /\ ( ps \/ th ) ) -> ta ) $=
      ( wo wa caselem jaoi sylbi ) ACJBDJKABKZCBKZJZADKZCDKZJZJEACBDLQETOEPFGMR
      ESHIMMN $.
      $([29-Jul-99]$)
  $}

  ${
    ccase2.1 $e |- ( ( ph /\ ps ) -> ta ) $.
    ccase2.2 $e |- ( ch -> ta ) $.
    ccase2.3 $e |- ( th -> ta ) $.
    $( Inference for combining cases. $)
    ccase2 $p |- ( ( ( ph \/ ch ) /\ ( ps \/ th ) ) -> ta ) $=
      ( adantr adantl ccase ) ABCDEFCEBGIDEAHJDECHJK $.
      $([29-Jul-99]$)
  $}

  ${
    4cases.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    4cases.2 $e |- ( ( ph /\ -. ps ) -> ch ) $.
    4cases.3 $e |- ( ( -. ph /\ ps ) -> ch ) $.
    4cases.4 $e |- ( ( -. ph /\ -. ps ) -> ch ) $.
    $( Inference eliminating two antecedents from the four possible cases that
       result from their true/false combinations. $)
    4cases $p |- ch $=
      ( wi exp wn pm2.61i ) BCABCHABCDIAJZBCFIKABJZCHAMCEILMCGIKK $.
      $([25-Oct-03]$)
  $}

  ${
    niabn.1 $e |- ph $.
    $( Miscellaneous inference relating falsehoods. $)
    niabn   $p |- ( -. ps -> ( ( ch /\ ps ) <-> -. ph ) ) $=
      ( wn wa pm2.21 adantld wi pm2.21ni a1i impbid ) BEZCBFZAEZMBOCBOGHONIMAND
      JKL $.
      $([31-Mar-94]$)
  $}

  $( Lemma for an alternate version of weak deduction theorem. $)
  dedlem0a $p |- ( ph -> ( ps <-> ( ( ch -> ph ) -> ( ps /\ ph ) ) ) ) $=
    ( wi wa ax-1 a1i syl4 comm impbid iba bilimd bitrd ) ABCADZBDZNBAEZDABOBODA
    BNFGOABANBACFHIJABPNABKLM $.
    $([2-Apr-94]$)

  $( Lemma for an alternate version of weak deduction theorem. $)
  dedlem0b $p |- ( -. ph -> ( ps <-> ( ( ps -> ph ) -> ( ch /\ ph ) ) ) ) $=
    ( wn wi wa pm2.21 syl3d com23 pm3.27 syl34 pm2.15d comm impbid ) ADZBBAEZCA
    FZEZOPBQOAQBAQGHIROBRBABDPQABAGCAJKLMN $.
    $([2-Apr-94]$)

  $( Lemma for weak deduction theorem. $)
  dedlema $p |- ( ph -> ( ps <-> ( ( ps /\ ph ) \/ ( ch /\ -. ph ) ) ) ) $=
    ( wn wa wo wi orc a1i idd pm2.21 comm adantld jaod impbid iba birord bitrd
    ) ABBCADZEZFZBAEZTFABUABUAGABTHIABBTABJASBCSABABKLMNOABUBTABPQR $.
    $([26-Jun-02]$)

  $( Lemma for weak deduction theorem. $)
  dedlemb $p |- ( -. ph -> ( ch <-> ( ( ps /\ ph ) \/ ( ch /\ -. ph ) ) ) ) $=
    ( wn wa wo pm3.21 olc syl6 wi pm2.21 com23 imp3a pm3.26 a1i jaod impbid )
    ADZCBAEZCREZFZRCTUARCGTSHIRSCTRBACRABCABCJKLMTCJRCRNOPQ $.
    $([15-May-99]$)

  ${
    elimh.1 $e |- ( ( ph <-> ( ( ph /\ ch ) \/ ( ps /\ -. ch ) ) )
                     -> ( ch <-> ta ) ) $.
    elimh.2 $e |- ( ( ps <-> ( ( ph /\ ch ) \/ ( ps /\ -. ch ) ) )
                     -> ( th <-> ta ) ) $.
    elimh.3 $e |- th $.
    $( Hypothesis builder for weak deduction theorem.  For more information, see
       the Deduction Theorem link on the Metamath Proof Explorer home page.  $)
    elimh $p |- ta $=
      ( wa wn wo wb dedlema syl ibi dedlemb mpbii pm2.61i ) CECECAACIBCJZIKZLCE
      LCABMFNOSDEHSBTLDELCABPGNQR $.
      $([26-Jun-02]$)
  $}

  ${
    dedt.1 $e |- ( ( ph <-> ( ( ph /\ ch ) \/ ( ps /\ -. ch ) ) )
                     -> ( th <-> ta ) ) $.
    dedt.2 $e |- ta $.
    $( The weak deduction theorem.  For more information, see the Deduction
       Theorem link on the Metamath Proof Explorer home page.  $)
    dedt $p |- ( ch -> th ) $=
      ( wa wn wo wb dedlema mpbiri syl ) CAACHBCIHJKZDCABLODEGFMN $.
      $([26-Jun-02]$)
  $}

  $( Contraposition.  Theorem *2.16 of [WhiteheadRussell] p. 103.  This version
     of ~ pm2.16 demonstrates the use of the weak deduction theorem to derive
     it from ~ con3 . $)
  con3th $p |- ( ( ph -> ps ) -> ( -. ps -> -. ph ) ) $=
    ( wi wn wa wo wb id binegd birimd bilimd elimh con3 dedt ) BAABCZBDZADZCBOE
    AODEFZDZQCBRGZPSQTBRTHZIJARBAOAACARCTBRAUAKARGZARAUBHKAHLMN $.
    $([27-Jun-02]$)

  $( The consensus theorem.  This theorem and its dual (with ` \/ ` and ` /\ `
     interchanged) are commonly used in computer logic design to eliminate
     redundant terms from Boolean expressions.  Specifically, we show the term
     ` ( ps /\ ch ) ` on the left-hand side is redundant. $)
  consensus $p |- ( ( ( ( ph /\ ps ) \/ ( -. ph /\ ch ) ) \/ ( ps /\ ch ) ) <->
                      ( ( ph /\ ps ) \/ ( -. ph /\ ch ) ) ) $=
    ( wa wn wo id wi dedlema biimpd adantrd dedlemb adantld pm2.61i ancom
    bi2or sylib jaoi orc impbi ) ABDZAEZCDZFZBCDZFUDUDUDUEUDGUEBADZCUBDZFZUDAUE
    UHHABUHCABUHABCIJKUBCUHBUBCUHABCLJMNUFUAUGUCBAOCUBOPQRUDUEST $.
    $([16-May-03]$)

  ${
    ninba.1 $e |- ph $.
    $( Miscellaneous inference relating falsehoods. $)
    ninba   $p |- ( -. ps -> ( -. ph <-> ( ch /\ ps ) ) ) $=
      ( wn wa wi pm2.21ni a1i pm2.21 adantld impbid ) BEZAEZCBFZNOGMAODHIMBNCBN
      JKL $.
      $([31-Mar-94]$)
  $}

  ${
    $v et $. $( Greek eta $)
    wet $f wff et $.
    prlem1.1 $e |- ( ph -> ( et <-> ch ) ) $.
    prlem1.2 $e |- ( ps -> -. th ) $.
    $( A specialized lemma for set theory (axiom of pairing). $)
    prlem1   $p |- ( ph -> ( ps ->
                  ( ( ( ps /\ ch ) \/ ( th /\ ta ) ) -> et ) ) ) $=
      ( wa wo wi biimprcd adantl a1d com23 wn pm2.21 comm syl5 adantr jaoi
      com3l ) BCIZDEIZJABFUCABFKZKUDUCBAFUCAFKZBCUFBAFCGLMNOUDUEADUEEDDPZFBUGDF
      DFQRHSTNUAUB $.
      $([18-Oct-95]$)
  $}

  $( A specialized lemma for set theory (axiom of pairing). $)
  prlem2 $p |- ( ( ( ph /\ ps ) \/ ( ch /\ th ) ) <->
               ( ( ph \/ ch ) /\ ( ( ph /\ ps ) \/ ( ch /\ th ) ) ) ) $=
    ( wa wo oel biran anass bitr orcom bi2or andi bitr4 ) ABEZCDEZFZACFZOEZRPEZ
    FRQEOSPTORAEZBESAUABACGHRABIJPRCEZDETCUBDCCAFZCEUBCAGUCRCCAKHJHRCDIJLROPMN
    $.
    $([5-Aug-93]$)

  ${
    oplem1.1 $e |- ( ph -> ( ps \/ ch ) ) $.
    oplem1.2 $e |- ( ph -> ( th \/ ta ) ) $.
    oplem1.3 $e |- ( ps <-> th ) $.
    oplem1.4 $e |- ( ch -> ( th <-> ta ) ) $.
    $( A specialized lemma for set theory (ordered pair theorem). $)
    oplem1   $p |- ( ph -> ps ) $=
      ( wn wi wa ord bineg syl5ib jcad syl5bb biimpar syl6 pm2.18 syl ) ABJZBKB
      AUBCELBAUBCEABCFMADJEUBADEGMBDHNOPCBECDEBIHQRSBTUA $.
      $([18-Oct-95]$)
  $}

  $( Lemma used in construction of real numbers. $)
  rnlem $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
  ( ( ( ph /\ ch ) /\ ( ps /\ th ) ) /\ ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) ) $=
    ( wa anandir anandi bi2an ancom bilan an4 bitr 3bitr ) ABECDEZEANEZBNEZEACE
    ZADEZEZBCEZBDEZEZEZQUAERTEEZABNFOSPUBACDGBCDGHUCSUATEZEUDUBUESTUAIJQRUATKLM
    $.
    $([4-Sep-95]$)

