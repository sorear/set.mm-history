$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                Mathbox for David A. Wheeler
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $( Eventually this should be defined with sin and cos $)

  $c sec $. $( Secant function. $)
  $c csc $. $( Cosecant function. $)
  $c cot $. $( Cotangent function. $)

  $( Extend class notation to include the secant function. $)
  csec $a class sec $.

  $( Extend class notation to include the cosecant function. $)
  ccsc $a class csc $.

  $( Extend class notation to include the cotangent function. $)
  ccot $a class cot $.

  ${
    $d x y $.
    $( Define the secant function.  We define it this way for ~ cmpt , which
       requires the form ` ( x e. A |-> B ) ` . $)
    df-sec $a |- sec = ( x e. { y e. CC | ( cos ` y ) =/= 0 } |->
                       ( 1 / ( cos ` x ) ) ) $.

    $( Define the cosecant function.  We define it this way for ~ cmpt , which
       requires the form ` ( x e. A |-> B ) ` . $)
    df-csc $a |- csc = ( x e. { y e. CC | ( sin ` y ) =/= 0 } |->
                       ( 1 / ( sin ` x ) ) ) $.

    $( Define the cotangent function.  We define it this way for ~ cmpt , which
       requires the form ` ( x e. A |-> B ) ` . $)
    df-cot $a |- cot = ( x e. { y e. CC | ( sin ` y ) =/= 0 } |->
                       ( ( cos ` x ) / ( sin ` x ) ) ) $.
  $}

  ${
    $d x y A $.
    $( Value of the secant function.  (Contributed by David A. Wheeler,
       14-Mar-2014.) $)
    secval $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) ->
                   ( sec ` A ) = ( 1 / ( cos ` A ) ) ) $=
      ( vy vx cc wcel ccos cfv cc0 wa cv crab csec c1 cdiv co wceq fveq2 neeq1d
      wne elrab opreq2d df-sec oprex fvmpt sylbir ) ADEAFGZHSZIABJZFGZHSZBDKZEA
      LGMUFNOZPUJUGBADUHAPUIUFHUHAFQRTCAMCJZFGZNOULUKLUMAPUNUFMNUMAFQUACBUBMUFN
      UCUDUE $.
      $( [14-Mar-2014] $)

    $( Value of the cosecant function.  (Contributed by David A. Wheeler,
       14-Mar-2014.) $)
    cscval $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 ) ->
                   ( csc ` A ) = ( 1 / ( sin ` A ) ) ) $=
      ( vy vx cc wcel csin cfv cc0 wa cv crab ccsc c1 cdiv co wceq fveq2 neeq1d
      wne elrab opreq2d df-csc oprex fvmpt sylbir ) ADEAFGZHSZIABJZFGZHSZBDKZEA
      LGMUFNOZPUJUGBADUHAPUIUFHUHAFQRTCAMCJZFGZNOULUKLUMAPUNUFMNUMAFQUACBUBMUFN
      UCUDUE $.
      $( [14-Mar-2014] $)

    $( Value of the cotangent function.  (Contributed by David A. Wheeler,
       14-Mar-2014.) $)
    cotval $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 ) ->
                   ( cot ` A ) = ( ( cos ` A ) / ( sin ` A ) ) ) $=
      ( vy vx cc wcel csin cfv cc0 wne wa cv crab ccot ccos cdiv co wceq neeq1d
      fveq2 elrab opreq12d df-cot oprex fvmpt sylbir ) ADEAFGZHIZJABKZFGZHIZBDL
      ZEAMGANGZUFOPZQUJUGBADUHAQUIUFHUHAFSRTCACKZNGZUNFGZOPUMUKMUNAQUOULUPUFOUN
      ANSUNAFSUACBUBULUFOUCUDUE $.
      $( [14-Mar-2014] $)
  $}

  $( The closure of the secant function with a complex argument.  (Contributed
     by David A. Wheeler, 14-Mar-2014.) $)
  seccl $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) ->
              ( sec ` A ) e. CC ) $=
    ( cc wcel ccos cfv cc0 wne wa csec c1 cdiv co secval ax-1cn a1i coscl simpr
    adantr divcl syl111anc eqeltrd ) ABCZADEZFGZHZAIEJUCKLZBAMUEJBCZUCBCZUDUFBC
    UGUENOUBUHUDAPRUBUDQJUCSTUA $.
    $( [14-Mar-2014] $)

  $( The closure of the cosecant function with a complex argument.
     (Contributed by David A. Wheeler, 14-Mar-2014.) $)
  csccl $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 ) ->
              ( csc ` A ) e. CC ) $=
    ( cc wcel csin cfv cc0 wne wa ccsc c1 cdiv co cscval ax-1cn a1i sincl simpr
    adantr divcl syl111anc eqeltrd ) ABCZADEZFGZHZAIEJUCKLZBAMUEJBCZUCBCZUDUFBC
    UGUENOUBUHUDAPRUBUDQJUCSTUA $.
    $( [14-Mar-2014] $)

  $( The closure of the cotangent function with a complex argument.
     (Contributed by David A. Wheeler, 15-Mar-2014.) $)
  cotcl $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 ) -> ( cot ` A ) e. CC ) $=
    ( cc wcel csin cfv cc0 wa ccot ccos cdiv co cotval coscl adantr sincl simpr
    wne divcl syl111anc eqeltrd ) ABCZADEZFQZGZAHEAIEZUBJKZBALUDUEBCZUBBCZUCUFB
    CUAUGUCAMNUAUHUCAONUAUCPUEUBRST $.
    $( [23-Mar-2014] $)

  $( The closure of the secant function with a real argument.  (Contributed by
     David A. Wheeler, 15-Mar-2014.) $)
  reseccl $p |- ( ( A e. RR /\ ( cos ` A ) =/= 0 ) -> ( sec ` A ) e. RR ) $=
    ( cr wcel ccos cfv cc0 wa csec c1 cdiv co cc wceq recn secval sylan recoscl
    wne 1re a1i redivcl syl3an1 syl3an2 3anidm12 eqeltrd ) ABCZADEZFRZGAHEZIUGJ
    KZBUFALCUHUIUJMANAOPUFUHUJBCZUFUFUGBCZUHUKAQUFIBCZULUHUKUMUFSTIUGUAUBUCUDUE
    $.
    $( [15-Mar-2014] $)

  $( The closure of the cosecant function with a real argument.  (Contributed
     by David A. Wheeler, 15-Mar-2014.) $)
  recsccl $p |- ( ( A e. RR /\ ( sin ` A ) =/= 0 ) -> ( csc ` A ) e. RR ) $=
    ( cr wcel csin cfv cc0 wa ccsc c1 cdiv co cc wceq recn cscval sylan resincl
    wne 1re a1i redivcl syl3an1 syl3an2 3anidm12 eqeltrd ) ABCZADEZFRZGAHEZIUGJ
    KZBUFALCUHUIUJMANAOPUFUHUJBCZUFUFUGBCZUHUKAQUFIBCZULUHUKUMUFSTIUGUAUBUCUDUE
    $.
    $( [23-Mar-2014] $)

  $( The closure of the cotangent function with a real argument.  (Contributed
     by David A. Wheeler, 15-Mar-2014.) $)
  recotcl $p |- ( ( A e. RR /\ ( sin ` A ) =/= 0 ) -> ( cot ` A ) e. RR ) $=
    ( cr wcel csin cfv cc0 wne wa ccot ccos cdiv co cc wceq recn cotval resincl
    sylan recoscl redivcl syl3an1 syl3an2 3anidm12 eqeltrd ) ABCZADEZFGZHAIEZAJ
    EZUFKLZBUEAMCUGUHUJNAOAPRUEUGUJBCZUEUEUFBCZUGUKAQUEUIBCULUGUKASUIUFTUAUBUCU
    D $.
    $( [23-Mar-2014] $)

  $( The reciprocal of secant is cosine.  (Contributed by David A. Wheeler,
     14-Mar-2014.) $)
  recsec $p |- ( ( A e. CC /\ ( cos ` A ) =/= 0 ) ->
                 ( cos ` A ) = ( 1 / ( sec ` A ) ) ) $=
    ( cc wcel ccos cfv cc0 wne wa c1 csec cdiv secval opreq2d wceq coscl recrec
    co sylan eqtr2d ) ABCZADEZFGZHZIAJEZKQIIUAKQZKQZUAUCUDUEIKALMTUABCUBUFUANAO
    UAPRS $.
    $( [14-Mar-2014] $)

  $( The reciprocal of cosecant is sine.  (Contributed by David A. Wheeler,
     14-Mar-2014.) $)
  reccsc $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 ) ->
               ( sin ` A ) = ( 1 / ( csc ` A ) ) ) $=
    ( cc wcel csin cfv cc0 wne wa c1 ccsc cdiv cscval opreq2d wceq sincl recrec
    co sylan eqtr2d ) ABCZADEZFGZHZIAJEZKQIIUAKQZKQZUAUCUDUEIKALMTUABCUBUFUANAO
    UAPRS $.
    $( [14-Mar-2014] $)


  $( The reciprocal of cotangent is tangent.  (Contributed by David A. Wheeler,
     21-Mar-2014.) $)
  reccot $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 /\ ( cos ` A ) =/= 0 ) ->
                   ( tan ` A ) = ( 1 / ( cot ` A ) ) ) $=
    ( cc wcel csin cfv cc0 wne ccos w3a c1 cdiv co ccot ctan sincl coscl recdiv
    wceq wa sylanl1 sylanr1 3impdi 3com23 cotval 3adant3 opreq2d tanval 3adant2
    3eqtr4rd ) ABCZADEZFGZAHEZFGZIZJUMUKKLZKLZUKUMKLZJAMEZKLANEZUJUNULUQURRZUJU
    NULVAUJUJUNSUKBCZULVAAOUJUMBCUNVBULSVAAPUMUKQTUAUBUCUOUSUPJKUJULUSUPRUNAUDU
    EUFUJUNUTURRULAUGUHUI $.
    $( [21-Mar-2014] $)

  $( The reciprocal of tangent is cotangent.  (Contributed by David A. Wheeler,
     21-Mar-2014.) $)
  rectan $p |- ( ( A e. CC /\ ( sin ` A ) =/= 0 /\ ( cos ` A ) =/= 0 ) ->
                   ( cot ` A ) = ( 1 / ( tan ` A ) ) ) $=
    ( cc wcel csin cfv cc0 wne ccos w3a c1 cdiv co ctan ccot coscl sincl recdiv
    wceq wa sylanl1 sylanr1 3impdi tanval 3adant2 opreq2d 3adant3 3eqtr4rd
    cotval ) ABCZADEZFGZAHEZFGZIZJUJULKLZKLZULUJKLZJAMEZKLANEZUIUKUMUPUQRZUIUIU
    KSULBCZUMUTAOUIUJBCUKVAUMSUTAPUJULQTUAUBUNURUOJKUIUMURUORUKAUCUDUEUIUKUSUQR
    UMAUHUFUG $.
    $( [21-Mar-2014] $)

  $( The value of the secant function at zero is one.  (Contributed by David A.
     Wheeler, 16-Mar-2014.) $)
  sec0 $p |- ( sec ` 0 ) = 1 $=
    ( cc0 csec cfv c1 ccos cdiv co cc wcel wne wceq cos0 ax-1ne0 eqnetri secval
    0cn mp2an opreq2i ax-1cn div1i 3eqtri ) ABCZDAECZFGZDDFGDAHIUCAJUBUDKPUCDAL
    MNAOQUCDDFLRDSTUA $.
    $( [16-Mar-2014] $)

$( (End of David A. Wheeler's mathbox.) $)


