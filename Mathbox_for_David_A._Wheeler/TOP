$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
                Mathbox for David A. Wheeler
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  $( Eventually this should be defined with sin and cos $)

  $c tan $. $( Tangent function. $)
  $c sec $. $( Secant function. $)
  $c csc $. $( Cosecant function. $)
  $c cot $. $( Cotangent function. $)

  $( Extend class notation to include the tangent function. $)
  ctan $a class tan $.

  $( Extend class notation to include the secant function. $)
  csec $a class sec $.

  $( Extend class notation to include the cosecant function. $)
  ccsc $a class csc $.

  $( Extend class notation to include the cotangent function. $)
  ccot $a class cot $.

  $( Define the tangent function.  We define it this way for ~ cmpt , which
     requires the form ` ( x e. A |-> B ) ` .  Its domain is the complex
     numbers whose cosine is not zero. $)
  df-tan $a |- tan = ( x e. ( `' cos " ( CC \ { 0 } ) ) |->
                         ( ( sin ` x ) / ( cos ` x ) ) ) $.

  $( Define the secant function.  We define it this way for ~ cmpt , which
     requires the form ` ( x e. A |-> B ) ` .  Its domain is the complex
     numbers whose cosine is not zero. $)
  df-sec $a |- sec = ( x e. ( `' cos " ( CC \ { 0 } ) ) |->
                         ( 1 / ( cos ` x ) ) ) $.

  $( Define the cosecant function.  We define it this way for ~ cmpt , which
     requires the form ` ( x e. A |-> B ) ` .  Its domain is the complex
     numbers whose sine is not zero. $)
  df-csc $a |- csc = ( x e. ( `' sin " ( CC \ { 0 } ) ) |->
                         ( 1 / ( sin ` x ) ) ) $.

  $( Define the cotangent function.  We define it this way for ~ cmpt , which
     requires the form ` ( x e. A |-> B ) ` .  Its domain is the complex
     numbers whose sine is not zero. $)
  df-cot $a |- cot = ( x e. ( `' sin " ( CC \ { 0 } ) ) |->
                         ( ( cos ` x ) / ( sin ` x ) ) ) $.

  ${
    $d x y $.
    $( Value of the tangent function.  Note that this proof has similarities
       with ~ sinval .  (Contributed by David A. Wheeler, 12-Mar-2014.) $)
    tanval $p |- ( x e. ( `' cos " ( CC \ { 0 } ) ) ->
                      ( tan ` x ) = ( ( sin ` x ) / ( cos ` x ) ) ) $=
      ( vy cv csin cfv ccos cdiv co ccnv cc cc0 csn cdif cima ctan weq opreq12d
      fveq2 df-tan oprex fvmpt ) BACZBCZDEZUCFEZGHUBDEZUBFEZGHFIJKLMNOBAPUDUFUE
      UGGUCUBDRUCUBFRQBSUFUGGTUA $.
      $( [12-Mar-2014] $)

    $( Value of the secant function.  (Contributed by David A. Wheeler,
       12-Mar-2014.) $)
    secval $p |- ( x e. ( `' cos " ( CC \ { 0 } ) ) ->
                      ( sec ` x ) = ( 1 / ( cos ` x ) ) ) $=
      ( vy cv c1 ccos cfv cdiv co ccnv cc0 csn cdif cima csec weq fveq2 opreq2d
      cc df-sec oprex fvmpt ) BACZDBCZEFZGHDUBEFZGHEIRJKLMNBAOUDUEDGUCUBEPQBSDU
      EGTUA $.
      $( [12-Mar-2014] $)

    $( Value of the cosecant function.  (Contributed by David A. Wheeler,
       12-Mar-2014.) $)
    cscval $p |- ( x e. ( `' sin " ( CC \ { 0 } ) ) ->
                      ( csc ` x ) = ( 1 / ( sin ` x ) ) ) $=
      ( vy cv c1 csin cfv cdiv co ccnv cc0 csn cdif cima ccsc weq fveq2 opreq2d
      cc df-csc oprex fvmpt ) BACZDBCZEFZGHDUBEFZGHEIRJKLMNBAOUDUEDGUCUBEPQBSDU
      EGTUA $.
      $( [12-Mar-2014] $)

    $( Value of the cotangent function.  (Contributed by David A. Wheeler,
       12-Mar-2014.) $)
    cotval $p |- ( x e. ( `' sin " ( CC \ { 0 } ) ) ->
                      ( cot ` x ) = ( ( cos ` x ) / ( sin ` x ) ) ) $=
      ( vy cv ccos cfv csin cdiv co ccnv cc cc0 csn cdif cima ccot weq opreq12d
      fveq2 df-cot oprex fvmpt ) BACZBCZDEZUCFEZGHUBDEZUBFEZGHFIJKLMNOBAPUDUFUE
      UGGUCUBDRUCUBFRQBSUFUGGTUA $.
      $( [12-Mar-2014] $)

  $}

$( (End of David A. Wheeler's mathbox.) $)


