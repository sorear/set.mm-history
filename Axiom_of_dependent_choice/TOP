$(
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
              Axiom of dependent choice
#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#*#
$)

  ${
    $d k s t u v x y z A $.  $d k s t u v x y z F $.  $d u v x y z G $.
    $d s t S $.  $d k s t x y z c $.  $d k s t u v x y z r $.
    acdc3lem.1 $e |- A e. _V $.
    acdc3lem.2 $e |- S = { <. <. x , y >. , z >. |
               ( ( x e. A /\ y e. A ) /\ z = U. { v e. ( F ` x ) |
                        A. u e. ( F ` x ) -. u r v } ) } $.
    acdc3lem.3 $e |- G = ( S seq1 ( NN X. { c } ) ) $.
    $( Lemma for ~ acdc3 .  Build a sequence ` G ` starting at value ` c ` , as
       follows.  Given a previous value ` x ` of ` G ` , we construct, for the
       next value of ` G ` , the ` v ` such that
       ` A. u e. ( F `` x ) -. u r v ` , which is unique when ` r ` is a
       well-ordering on ` A ` . $)
    acdc3lem $p |- ( ( r We A /\ ( c e. A /\ F : A --> ( ~P A \ { (/) } ) ) )
                 -> ( G : NN --> A /\ ( G ` 1 ) = c /\
       A. k e. NN ( G ` ( k + 1 ) ) e. ( F ` ( G ` k ) ) ) ) $=
      ( vs vt cv wwe wcel cpw c0 csn cdif wf wa cn c1 cfv wceq caddc co wral
      cxp cseq1 wss visset fconst snssi fss sylancr ad2antrl wfn wbr wn crab
      cuni fvex rabex uniex fveq2 rabeq raleq rabbidv eqtrd syl unieqd eqidd
      oprabval2 adantl ffvelrn eldifi adantll elpwi wne simpll eldifn id 0ex
      snid syl6eqel necon3bi wereucl syl111anc sseldd adantlrl adantrr eqeltrd
      ex r19.21aivv fnoprab2 jctil ffnoprv sylibr oprabex2 nnex snex xpex
      seq1f syl11anc feq1i fveq1i seq11 1nn fvconst2 ax-mp eqtri a1i seq1p1
      opreq1i peano2nn opreq2d simpr adantr ad2antlr seq1cl syl5eqel simplrl
      syl5eqr syl5eq simplrr r19.21aiva 3jca ) FKRZSZLRZFTZFFUAZUBUCZUDZIUEZUFZ
      UFZUGFJUEZUHJUIZYPUJZHRZUHUKULZJUIZUUGJUIZIUIZTZHUGUMUUCUGFGUGYPUCZUNZUOU
      LZUEZUUDUUCUGFUUNUEZFFUNZFGUEZUUPYQUUQYOUUAYQUGUUMUUNUEUUMFUPUUQUGYPLUQZU
      RYPFUSUGUUMFUUNUTVAZVBUUCGUURVCZPRZQRZGULZFTZQFUMPFUMZUFUUSUUCUVGUVBUUCUV
      FPQFFUUCUVCFTZUVDFTZUFZUVFUUCUVJUFUVEERDRYNVDVEZEUVCIUIZUMZDUVLVFZVGZFUVJ
      UVEUVOUJUUCABCUVCUVDFFUVKEARZIUIZUMZDUVQVFZVGZUVOGUVOUVNUVMDUVLUVCIVHZVIV
      JUVPUVCUJZUVSUVNUWBUVQUVLUJZUVSUVNUJUVPUVCIVKUWCUVSUVRDUVLVFUVNUVRDUVQUVL
      VLUWCUVRUVMDUVLUVKEUVQUVLVMVNVOVPVQBRZUVDUJUVOVRNVSVTUUCUVHUVOFTZUVIYOUUA
      UVHUWEYQYOUUAUFUVHUFZUVLFUVOUWFUVLYRTZUVLFUPZUUAUVHUWGYOUUAUVHUFZUVLYTTZU
      WGFYTUVCIWAZUVLYRYSWBVPWCUVLFWDVPZUWFYOUWHUVLUBWEZUVOUVLTYOUUAUVHWFUWLUUA
      UVHUWMYOUWIUWJUWMUWKUWJUVLYSTZVEUWMUVLYRYSWGUWNUVLUBUVLUBUJZUVLUBYSUWOWHU
      BWIWJZWKWLVPVPWCDEFUVLYNUWAWMWNWOWPWQWRWSWTABCFFUVTGUVSUVRDUVQUVPIVHVIVJN
      XAXBPQFFFGXCXDZFGUUNABCFFUVTGMMNXEZUGUUMXFYPXGXHZXIXJUGFJUUOOXKXDUUFUUCUU
      EUHUUOUIZYPUHJUUOOXLUWTUHUUNUIZYPGUUNUWRUWSXMUHUGTUXAYPUJXNUGYPUHUUTXOXPX
      QXQXRUUCUULHUGUUCUUGUGTZUFZUUIUVKEUUKUMZDUUKVFZVGZUUKUXCUUIUUHUUOUIZUXFUU
      HJUUOOXLUXCUXGUUGUUOUIZUUHUUNUIZGULZUXFUXBUXGUXJUJUUCUUGGUUNUWRUWSXSVTUXC
      UXJUUJUXIGULZUXFUUJUXHUXIGUUGJUUOOXLZXTUXCUXKUUJYPGULZUXFUXBUXKUXMUJUUCUX
      BUXIYPUUJGUXBUUHUGTUXIYPUJUUGYAUGYPUUHUUTXOVPYBVTUXCUUJFTZYQUXMUXFUJUXCUU
      JUXHFUXLUXCUXBUUQUUSUXHFTUUCUXBYCUUBUUQYOUXBYQUUQUUAUVAYDYEUUCUUSUXBUWQYD
      UUGFGUUNUWRUWSYFWNYGZYOYQUUAUXBYHABCUUJYPFFUVTUXFGUXFUXEUXDDUUKUUJIVHZVIV
      JUVPUUJUJZUVSUXEUXQUVQUUKUJZUVSUXEUJUVPUUJIVKUXRUVSUVRDUUKVFUXEUVRDUVQUUK
      VLUXRUVRUXDDUUKUVKEUVQUUKVMVNVOVPVQUWDYPUJUXFVRNVSXJVOYIVOYJUXCYOUUKFUPZU
      UKUBWEZUXFUUKTYOUUBUXBWFUXCUUKYRTZUXSUXCUUKYTTZUYAUXCUUAUXNUYBYOYQUUAUXBY
      KUXOFYTUUJIWAXJZUUKYRYSWBVPUUKFWDVPUXCUYBUXTUYCUYBUUKYSTZVEUXTUUKYRYSWGUY
      DUUKUBUUKUBUJZUUKUBYSUYEWHUWPWKWLVPVPDEFUUKYNUXPWMWNWRYLYM $.
      $( [8-Oct-2007] $)
  $}

  ${
    $d a b c d g k r u v x y z A $.  $d c g C $.
    $d a b c d f g h k r u v x y z F $.
    acdc3.1 $e |- A e. _V $.
    $( Dependent Choice.  Axiom DC1 of [Schechter] p. 149, with the addition of
       an initial value ` C ` .  This theorem is weaker than the Axiom of
       Choice but is stronger than Countable Choice.  It shows the existence of
       a sequence whose values can only be shown to exist (but cannot be
       constructed explicitly) and also depend on earlier values in the
       sequence.  TODO: clean up acdc* stuff. $)
    acdc3 $p |- ( ( F : A --> ( ~P A \ { (/) } ) /\ C e. A ) ->
                E. g ( g : NN --> A /\ ( g ` 1 ) = C /\
                  A. k e. NN ( g ` ( k + 1 ) ) e. ( F ` ( g ` k ) ) ) ) $=
      ( vc vr va vb vd vh vf vx vy vz vv vu wcel cpw c0 csn cdif wf cn cv c1
      cfv wceq caddc co wral w3a wex wi eqeq2 3anbi2d exbidv imbi2d wwe cvv
      weth ax-mp wa wbr wn crab cuni copab2 cxp cseq1 eleq1 bi2anan9 fveq2
      rabeq raleq rabbidv eqtrd syl adantr breq2 notbid ralbidv breq1 cbvralv
      syl6bb cbvrabv syl6eq unieqd eqeq2d anbi12d cbvoprab12v eqeq1 anbi2d
      cbvoprab3v eqtri eqid acdc3lem oprex feq1 fveq1 eqeq1d fveq2d eleq12d
      3anbi123d cla4ev exp32 19.23aiv vtoclga impcom ) BASAATUAUBUCEUDZUEACUFZU
      DZUGXLUHZBUIZDUFZUGUJUKZXLUHZXPXLUHZEUHZSZDUEULZUMZCUNZXKXMXNGUFZUIZYBUMZ
      CUNZUOZXKYDUOGBAYEBUIZYHYDXKYJYGYCCYJYFXOXMYBYEBXNUPUQURUSAHUFZUTZHUNZYEA
      SZYIUOZAVASYMFHAVAVBVCYLYOHYLYNXKYHYLYNXKVDVDUEAIUFZASZJUFZASZVDZKUFZLUFZ
      MUFZYKVEZVFZLYPEUHZULZMUUFVGZVHZUIZVDZIJKVIZUEYEUBVJZVKUKZUDZUGUUNUHZYEUI
      ZXQUUNUHZXPUUNUHZEUHZSZDUEULZUMZYHNOPQRAUULDEUUNHGFUULNUFZASZOUFZASZVDZUU
      ARUFZQUFZYKVEZVFZRUVDEUHZULZQUVMVGZVHZUIZVDZNOKVIUVHPUFZUVPUIZVDZNOPVIUUK
      UVRIJKNOYPUVDUIZYRUVFUIZVDZYTUVHUUJUVQUWBYQUVEUWCYSUVGYPUVDAVLYRUVFAVLVMU
      WDUUIUVPUUAUWDUUHUVOUWDUUHUUELUVMULZMUVMVGZUVOUWBUUHUWFUIZUWCUWBUUFUVMUIZ
      UWGYPUVDEVNUWHUUHUUGMUVMVGUWFUUGMUUFUVMVOUWHUUGUWEMUVMUUELUUFUVMVPVQVRVSV
      TUWEUVNMQUVMUUCUVJUIZUWEUUBUVJYKVEZVFZLUVMULUVNUWIUUEUWKLUVMUWIUUDUWJUUCU
      VJUUBYKWAWBWCUWKUVLLRUVMUUBUVIUIUWJUVKUUBUVIUVJYKWDWBWEWFWGWHWIWJWKWLUVRU
      WANOKPUUAUVSUIUVQUVTUVHUUAUVSUVPWMWNWOWPUUNWQWRYGUVCCUUNUULUUMVKWSXLUUNUI
      ZXMUUOYFUUQYBUVBUEAXLUUNWTUWLXNUUPYEUGXLUUNXAXBUWLYAUVADUEUWLXRUURXTUUTXQ
      XLUUNXAUWLXSUUSEXPXLUUNXAXCXDWCXEXFVSXGXHVCXIXJ $.
      $( [8-Oct-2007] $)
  $}

  ${
    $d a b c d h g j k r u v x y z A $.  $d a b c d f g h j k r u v x y z F $.
    $d h k B $.  $d g h k C $.  $d h k G $.
    acdc4lem.1 $e |- A e. _V $.
    acdc4lem.2 $e |- G = { <. <. j , y >. , z >. | ( ( j e. NN /\ y e. A ) /\
                  z = ( { ( j + 1 ) } X. ( ( j + 1 ) F y ) ) ) } $.
    acdc4lem.3 $e |- B = ( NN X. A ) $.
    acdc4lem.4 $e |- H = ( 1st o. h ) $.
    $( Lemma for acdc4 . $)
    acdc4lem1 $p |- ( ( G : B --> ( ~P B \ { (/) } ) /\ C e. A ) ->
                E. h ( h : NN --> B /\ ( h ` 1 ) = <. 1 , C >. /\
                  A. k e. NN ( h ` ( k + 1 ) ) e. ( G ` ( h ` k ) ) ) ) $=
      ( wcel cpw c0 csn cdif wf c1 cop cn cv cfv wceq caddc co wral w3a wex
      cxp 1nn opelxpi mpan a1i eqcomd eleqtrd cvv nnex xpex eqeltri acdc3
      sylan2 ) ECPZDDQRSTJUAUBEUCZDPUDDFUEZUAUBVHUFVGUGHUEZUBUHUIVHUFVIVHUFJUFP
      HUDUJUKFULVFVGUDCUMZDUBUDPVFVGVJPUNUBEUDCUOUPVFDVJDVJUGVFNUQURUSDVGFHJDVJ
      UTNUDCVALVBVCVDVE $.
      $( [24-May-2009] $) $( [8-Oct-2007] $)

$(
    @( Lemma for acdc4 . @)
    acdc4lem3 @p |- ( ( F : ( NN X. A ) --> ( ~P A \ { (/) } ) /\ C e. A ) ->
               E. g ( g : NN --> A /\ ( g ` 1 ) = C /\
           A. k e. NN ( g ` ( k + 1 ) ) e. ( ( k + 1 ) F ( g ` k ) ) ) ) @=
      ( cn cxp cpw c0 csn cdif wf wcel wa cv c1 cfv cop wceq caddc co wral w3a
      wex acdc4lem1 wss wne peano2nn snssi syl adantr adantl foprrn syl3an2OLD
      eldifi oprex elpw sylib 3expb jca xpss12 visset snnz a1i wn eldifn elsnc
      necon3bbii bicomi biimpri xpnz eldif snex xpex anbi12i bitri sylibr ex
   r19.21aivv foprab2 wb feq2 pweq difeq1d feq3 bitrd ax-mp sylanOLD c2nd ccom
      wi cres f2ndres reseq2 feq1 mpbir fco sylancom crn frn cores feq1d mpbid
      wfun cvv wfo fo2nd fofun 1nn fvco3 mp3an3 mpan fveq2 op2ndg eqtrd
      adantlr adantll mp3an1 sylan2OLD c1st wrel 1st2nd relxp ffvelrn eleqtrd
      sylancrOLD fveq2d xpeq1 xpeq2 id opreq2d oprabval2 sylancOLD df-opr
      eqcomi
      syl5eqOLD eleq2d biimpd imp elxp6 simprbi simprd eqcomd eqeltrd ralimdva
      3anim123d 3expd pm2.43d 3impd cofunexg mp2an fveq1 eqeq1d eleq12d
      ralbidv 3anbi123d cla4ev syl6 19.23adv mpd ) QCRZCSZTUAZUBZJUCZECUDZUEZQD
      GUFZUCZUGUVSUHZUGEUIZUJZIUFZUGUKULZUVSUHZUWDUVSUHZKUHZUDZIQUMZUNZGUOZQCFU
      FZUCZUGUWMUHZEUJZUWEUWMUHZUWEUWDUWMUHZJULZUDZIQUMZUNZFUOZDDSZUVNUBZKUCZUV
      QUWLUVPABCDEGHIJKLMNOPUPUVPUVLUVLSZUVNUBZKUCZUXFUVPHUFZUGUKULZUAZUXKAUFZJ
      ULZRZUXHUDZACUMHQUMUXIUVPUXPHAQCUVPUXJQUDZUXMCUDZUEZUXPUVPUXSUEZUXOUVLUQZ
      UXOTURZUEZUXPUXTUYAUYBUXTUXLQUQZUXNCUQZUEUYAUXTUYDUYEUXSUYDUVPUXQUYDUXRUX
      QUXKQUDZUYDUXJUSZUXKQUTVAVBVCUVPUXQUXRUYEUVPUXQUXRUNZUXNUVOUDZUYEUVPUYFUX
      RUYIUXQUXKUXMUVOQCJVDUYGVEZUYIUXNUVMUDUYEUXNUVMUVNVFUXNCUXKUXMJVGZVHVIVAV
      JVKUXLQUXNCVLVAUXTUXLTURZUXNTURZUEUYBUXTUYLUYMUYLUXTUXK?VMVNVOUVPUXQUXRUY
      MUYHUYIUYMUYJUYIUXNUVNUDZVPZUYMUXNUVMUVNVQUYMUYOUYOUYMUYNUXNTUXNTUYKVRVSV
      TWAVAVAVJVKUXLUXNWBVIVKUXPUXOUXGUDZUXOUVNUDZVPZUEUYCUXOUXGUVNWCUYPUYAUYRU
      YBUXOUVLUXLUXNUXKWDUYKWEZVHUYQUXOTUXOTUYSVRVSWFWGWHWIWJHABQCUXOUXHKNWKVIU
      XFUXIDUVLUJZUXFUXIWLOUYTUXFUVLUXEKUCZUXIDUVLUXEKWMUYTUXEUXHUJVUAUXIWLUYTU
      XDUXGUVNDUVLWNWOUXEUXHUVLKWPVAWQWRVTVIWSUVRUWKUXCGUVRUWKQCWTUVSXAZUCZUGVU
      BUHZEUJZUWEVUBUHZUWEUWDVUBUHZJULZUDZIQUMZUNZUXCUVRUWKVUKUVRUWKVUKUVRUVTUW
      CUWJVUKUVRUVTUWCUWJVUKXBXBZUVRUVTUVTVULXBUVRUVTUEZUVTUWCUWJVUKVUMUVTVUCUW
      CVUEUWJVUJVUMUVTVUCVUMUVTUEZQCWTDXCZUVSXAZUCZVUCVUMUVTDCVUOUCZVUQVURVUNVU
      RUVLCWTUVLXCZUCZQCXDVURDCVUSUCZVUTVUOVUSUJZVURVVAWLUYTVVBODUVLWTXEWRDCVUO
      VUSXFWRUYTVVAVUTWLODUVLCVUSWMWRWGXGVOQDCVUOUVSXHXIVUNQCVUPVUBVUNUVSXJDUQZ
      VUPVUBUJVUMVVCUVTUVTVVCUVRQDUVSXKVCVBWTUVSDXLVAXMXNWIUVQUVTUWCVUEXBUVPUVQ
      UVTUEZUWCVUEVVDUWCUEVUDUWAWTUHZEVVDVUDVVEUJZUWCUVTVVFUVQWTXOZUVTVVFXPXPWT
      XQVVGXRXPXPWTXSWRZVVGUVTUGQUDZVVFXTQDUGWTUVSYAYBYCVCVBUVQUWCVVEEUJUVTUVQU
      WCUEVVEUWBWTUHZEUWCVVEVVJUJUVQUWAUWBWTYDVCUVQVVJEUJZUWCVVIUVQVVKXTUGEQCYE
      YCVBYFYGYFWIYHVUMUWIVUIIQVUMUWDQUDZUEZUWIVUIVVMUWIUEZVUFUWFWTUHZVUHVVMVUF
      VVOUJZUWIUVTVVLVVPUVRUVTUWEQUDZVVPVVLVVGUVTVVQVVPVVHQDUWEWTUVSYAYIUWDUSYJ
      YHVBVVNVVOUWEUWGWTUHZJULZVUHVVN?VVOVVSUDZVVMUWI?UVTVVL?UVRUVTVVLUEZUWI?VW
      AUWH?UWFVWAUWHUWGYKUHZVVRUIZKUHZ?VWAUWGVWCKUVLYLUWGUVLUDUWGVWCUJVWAUWGUVL
      YMQCYNVWAUWGDUVLQDUWDUVSYOUYTVWAOVOYPYQYRVWAVWBVVRKULZ?VWDVWBQUDVVRCUD?VW
      AHABVWBVVRQCUXO?K??UXJVWBUJZUXO??VWF???UXL?UXNYSVAVWFUXNUWEUXMJULZUJ??UXN
      VWG?YTVAYFUXMVVRUJZVWGVVSUJ?VWHUXMVVRUWEJVWHUUAUUBVWGVVS?YTVANUUC??UUDVWE
      VWDVWBVVRKUUEUUFUUGYFUUHUUIYHUUJ??VVT?UWFUWFYKUHVVOUIUJ?UWF?VVSUUKUULUUMV
      AVVNVUHVVSVVMVUHVVSUJZUWIUVTVVLVWIUVRVWAVUGVVRUWEJVVGUVTVVLVUGVVRUJVVHQDU
      WDWTUVSYAYIUUBYHVBUUNYPUUOWIUUPUUQUURWIUUSUUTUUJWIUXBVUKFVUBVVGUVSXPUDVUB
      XPUDVVHGVMWTUVSXPUVAUVBUWMVUBUJZUWNVUCUWPVUEUXAVUJQCUWMVUBXFVWJUWOVUDEUGU
      WMVUBUVCUVDVWJUWTVUIIQVWJUWQVUFUWSVUHUWEUWMVUBUVCVWJUWRVUGUWEJUWDUWMVUBUV
      CUUBUVEUVFUVGUVHUVIUVJUVK @.
      @( [ ?] @) @( [8-Oct-2007] @)
$)


  $}

$(
  @{
    @d a b c d g k r u v x y z A @.  @d a b c d f g h k r u v x y z F @.
    acdc4.1 @e |- A e. _V @.
    @( A more general version of ~ acdc that allows the function ` F ` to vary
       with ` k ` . @)
    acdc4 @p |- ( ( F : ( NN X. A ) --> ( ~P A \ { (/) } ) /\ C e. A ) ->
               E. g ( g : NN --> A /\ ( g ` 1 ) = C /\
           A. k e. NN ( g ` ( k + 1 ) ) e. ( ( k + 1 ) F ( g ` k ) ) ) ) @=
      (  ) ? @.
      @( [ ?] @) @( [8-Oct-2007] @)
  @}
$)

  ${
    $d k s t u v x y z A $.  $d k s t u v x y z F $.  $d u v x y z G $.
    $d s t S $.  $d k s t x y z c $.  $d k s t u v x y z r $.
    $d u v x y z K $.  $d u v x y z X $.
    acdc2lem.1 $e |- A e. _V $.
    acdc2lem.2 $e |- S = { <. <. x , y >. , z >. |
               ( ( x e. A /\ y e. NN ) /\ z = U. { v e. ( y F x ) |
                        A. u e. ( y F x ) -. u r v } ) } $.
    acdc2lem.3 $e |- G =
              ( S seq1 ( { <. 1 , c >. } u. ( _I |` ( NN \ { 1 } ) ) ) ) $.
    $( Lemma for ~ acdc2 . $)
    acdc2lem1 $p |- ( ( ( r We A /\ F : ( NN X. A ) --> ( ~P A \ { (/) } ) )
                  /\ ( X e. A /\ K e. NN ) ) ->
             ( ( X S K ) e. ( K F X ) /\ ( X S K ) e. A ) ) $=
      ( cv wwe cn cxp cpw c0 csn cdif wf wa wcel co wbr wn wral crab cuni wceq
      oprex rabex uniex opreq2 rabeq raleq rabbidv eqtrd syl unieqd opreq1
      oprabval2 adantl wss wne simpll foprrn 3com23 3expb eldifi elpwi adantll
      eldifn id 0ex snid syl6eqel necon3bi wereucl syl111anc eqeltrd sseldd
      jca ) FLQZRZSFTFUAZUBUCZUDZHUEZUFZKFUGZJSUGZUFZUFZKJGUHZJKHUHZUGWSFUGWRWS
      EQDQWHUIUJZEWTUKZDWTULZUMZWTWQWSXDUNWNABCKJFSXAEBQZAQZHUHZUKZDXGULZUMXDGX
      AEXEKHUHZUKZDXJULZUMXCXBDWTJKHUOZUPUQXFKUNZXIXLXNXGXJUNZXIXLUNXFKXEHURXOX
      IXHDXJULXLXHDXGXJUSXOXHXKDXJXAEXGXJUTVAVBVCVDXEJUNZXLXCXPXJWTUNZXLXCUNXEJ
      KHVEXQXLXKDWTULXCXKDXJWTUSXQXKXBDWTXAEXJWTUTVAVBVCVDOVFVGWRWIWTFVHZWTUBVI
      ZXDWTUGWIWMWQVJWMWQXRWIWMWQUFZWTWJUGZXRXTWTWLUGZYAWMWOWPYBWMWPWOYBJKWLSFH
      VKVLVMZWTWJWKVNVCWTFVOVCVPZWMWQXSWIXTYBXSYCYBWTWKUGZUJXSWTWJWKVQYEWTUBWTU
      BUNZWTUBWKYFVRUBVSVTWAWBVCVCVPDEFWTWHXMWCWDWEZWRWTFWSYDYGWFWG $.
      $( [30-Sep-2007] $)

    $( Lemma for ~ acdc2 .  Build a sequence ` G ` starting at value ` c ` , as
       follows.  Given a previous value ` x ` of ` G ` , we construct, for the
       next value of ` G ` , the ` v ` such that
       ` A. u e. ( y F x ) -. u r v ` , which is unique when ` r ` is a
       well-ordering on ` A ` . $)
    acdc2lem2 $p |- ( ( r We A /\ ( c e. A /\
     F : ( NN X. A ) --> ( ~P A \ { (/) } ) ) ) -> ( G : NN --> A /\
          A. k e. NN ( G ` ( k + 1 ) ) e. ( ( k + 1 ) F ( G ` k ) ) ) ) $=
      ( vs vt cv wwe wcel cn cxp cpw c0 csn cdif wf wa c1 caddc co cfv wral
      cop cid cres cun cseq1 1nn elisseti visset eqid fvsnun1 id syl5eqel
      ad2antrl wss wf1o f1oi f1of ax-mp difss fss mp2an resundir cin wceq
      disjdif wfn wb fnsn fnresdisj mpbi residm uneq12i uncom un0 eqtr3i
      3eqtri feq1i mpbir jctir acdc2lem1 simprd ex r19.21aivv wbr wn crab cuni
      oprex rabex uniex fnoprab2 jctil ffnoprv sylibr adantrl nnex oprabex2
      snex cvv difexg resiexg unex seq1f2 3expa syl11anc seq1p1 seq1lem2
      fvsnun2 syl fvi syl6eq opreq2d eqtrd fveq1i opreq1i 3eqtr4g adantl
      simpll simplrr simpr simplrl adantr w3a simp2 a1i simp3 simp1 seq1cl2
      syl31anc syl111anc peano2nn simpld syl22anc eqeltrd r19.21aiva jca ) FKRZ
      SZLRZFTZUAFUBFUCUDUEUFIUGZUHZUHZUAFJUGZHRZUIUJUKZJULZUUSUURJULZIUKZTZHUAU
      MUUPUAFGUIUULUNZUEZUOUAUIUEZUFZUPZUQZURUKZUGZUUQUUPUIUVIULZFTZUVGUAUVIUVG
      UPZUGZUHFUAUBZFGUGZUVKUUPUVMUVOUUMUVMUUKUUNUUMUVLUULFUIUULUAUOUVIUIUAUSUT
      ZLVAZUVIVBZVCZUUMVDVEVFUVOUVGUAUVHUGZUVGUVGUVHUGZUVGUAVGUWBUVGUVGUVHVHUWC
      UVGVIUVGUVGUVHVJVKUAUVFVLUVGUVGUAUVHVMVNUVGUAUVNUVHUVNUVEUVGUPZUVHUVGUPZU
      QUDUVHUQZUVHUVEUVHUVGVOUWDUDUWEUVHUVFUVGVPUDVQZUWDUDVQZUVFUAVRUVEUVFVSUWG
      UWHVTUIUULUVRWAUVFUVGUVEWBVKWCUOUVGWDWEUVHUDUQUWFUVHUVHUDWFUVHWGWHWIWJWKZ
      WLUUKUUNUVQUUMUUKUUNUHZGUVPVSZPRZQRZGUKZFTZQUAUMPFUMZUHUVQUWJUWPUWKUWJUWO
      PQFUAUWJUWLFTUWMUATUHZUWOUWJUWQUHUWNUWMUWLIUKTUWOABCDEFGIJUWMUWLKLMNOWMWN
      WOWPABCFUAERDRUUJWQWREBRZARZIUKZUMZDUWTWSZWTZGUXBUXADUWTUWRUWSIXAXBXCNXDX
      EPQFUAFGXFXGXHZUVMUVOUVQUVKFUAGUVIABCFUAUXCGMXINXJZUVEUVHUVDXKUVGXLTZUVHX
      LTUAXLTUXFXIUAUVFXLXMVKUVGXLXNVKXOZXPXQXRUAFJUVJOWJXGUUPUVCHUAUUPUURUATZU
      HZUUTUVAUUSGUKZUVBUXHUUTUXJVQUUPUXHUUSUVJULZUURUVJULZUUSGUKZUUTUXJUXHUXKU
      XLUUSUVIULZGUKUXMUURGUVIUXEUXGXSUXHUXNUUSUXLGUXHUXNUUSUOULZUUSUXHUUSUVGTU
      XNUXOVQUURXTUIUULUAUUSUOUVIUVRUVSUVTYAYBUUSXLTUXOUUSVQUURUIUJXAUUSXLYCVKY
      DYEYFUUSJUVJOYGUVAUXLUUSGUURJUVJOYGZYHYIYJUXIUUKUUNUVAFTZUUSUATZUXJUVBTZU
      UKUUOUXHYKUUKUUMUUNUXHYLUXIUVAUXLFUXPUXIUXHUUMUVQUXLFTZUUPUXHYMUUKUUMUUNU
      XHYNUUPUVQUXHUXDYOUXHUUMUVQYPZUVMUVOUVQUXHUXTUYAUVLUULFUWAUXHUUMUVQYQVEUV
      OUYAUWIYRUXHUUMUVQYSUXHUUMUVQYTUURFUAGUVIUXEUXGUUAUUBUUCVEUXHUXRUUPUURUUD
      YJUWJUXQUXRUHUHUXSUXJFTABCDEFGIJUUSUVAKLMNOWMUUEUUFUUGUUHUUI $.
      $( [23-Sep-2007] $)
  $}

  ${
    $d a b c d g k r u v x y z A $.  $d a b c d f g h k r u v x y z F $.
    acdc2.1 $e |- A e. _V $.
    $( A more general version of ~ acdc that allows the function ` F ` to vary
       with ` k ` . $)
    acdc2 $p |- ( ( A =/= (/) /\ F : ( NN X. A ) --> ( ~P A \ { (/) } ) ) ->
               E. g ( g : NN --> A /\
           A. k e. NN ( g ` ( k + 1 ) ) e. ( ( k + 1 ) F ( g ` k ) ) ) ) $=
      ( vr vc va vb vd vh vf vx vy vz vv vu cv wwe wex c0 wne cn cxp cpw csn
      cdif wf wa c1 caddc co cfv wcel wral wi cvv weth ax-mp n0 wbr wn crab
      cuni wceq copab2 cop cid cres cun cseq1 eleq1 bi2anan9 opreq12 ancoms
      rabeq raleq rabbidv eqtrd syl breq2 notbid ralbidv breq1 cbvralv syl6bb
      cbvrabv syl6eq unieqd eqeq2d anbi12d cbvoprab12v eqeq1 anbi2d cbvoprab3v
      eqtri eqid acdc2lem2 oprex feq1 fveq1 opreq2d eleq12d cla4ev exp32
      19.23adv syl5bi imp3a 19.23aiv ) AFRZSZFTZAUAUBZUCAUDAUEUAUFUGDUHZUIUCABR
      ZUHZCRZUJUKULZXOUMZXRXQXOUMZDULZUNZCUCUOZUIZBTZUPZAUQUNXLEFAUQURUSXKYFFXK
      XMXNYEXMGRZAUNZGTXKXNYEUPZGAUTXKYHYIGXKYHXNYEXKYHXNUIUIUCAHRZAUNZIRZUCUNZ
      UIZJRZKRZLRZXJVAZVBZKYLYJDULZUOZLYTVCZVDZVEZUIZHIJVFZUJYGVGUFVHUCUJUFUGVI
      VJZVKULZUHZXRUUHUMZXRXQUUHUMZDULZUNZCUCUOZUIZYEMNOPQAUUFCDUUHFGEUUFMRZAUN
      ZNRZUCUNZUIZYOQRZPRZXJVAZVBZQUURUUPDULZUOZPUVEVCZVDZVEZUIZMNJVFUUTORZUVHV
      EZUIZMNOVFUUEUVJHIJMNYJUUPVEZYLUURVEZUIZYNUUTUUDUVIUVNYKUUQUVOYMUUSYJUUPA
      VLYLUURUCVLVMUVPUUCUVHYOUVPUUBUVGUVPUUBYSKUVEUOZLUVEVCZUVGUVPYTUVEVEZUUBU
      VRVEUVOUVNUVSYLUURYJUUPDVNVOUVSUUBUUALUVEVCUVRUUALYTUVEVPUVSUUAUVQLUVEYSK
      YTUVEVQVRVSVTUVQUVFLPUVEYQUVBVEZUVQYPUVBXJVAZVBZKUVEUOUVFUVTYSUWBKUVEUVTY
      RUWAYQUVBYPXJWAWBWCUWBUVDKQUVEYPUVAVEUWAUVCYPUVAUVBXJWDWBWEWFWGWHWIWJWKWL
      UVJUVMMNJOYOUVKVEUVIUVLUUTYOUVKUVHWMWNWOWPUUHWQWRYDUUOBUUHUUFUUGVKWSXOUUH
      VEZXPUUIYCUUNUCAXOUUHWTUWCYBUUMCUCUWCXSUUJYAUULXRXOUUHXAUWCXTUUKXRDXQXOUU
      HXAXBXCWCWKXDVTXEXFXGXHXIUS $.
      $( [29-Sep-2007] $)
  $}

  ${
    $d k s t u v x y z A $.  $d k s t u v x y z F $.  $d u v x y z G $.
    $d s t S $.  $d k s t x y z c $.  $d k s t u v x y z r $.
    $d u v x y z K $.  $d u v x y z X $.
    acdc5lem.1 $e |- A e. _V $.
    acdc5lem.2 $e |- S = { <. <. x , y >. , z >. |
               ( ( x e. A /\ y e. NN ) /\ z = U. { v e. ( y F x ) |
                        A. u e. ( y F x ) -. u r v } ) } $.
    acdc5lem.3 $e |- G =
              ( S seq1 ( { <. 1 , c >. } u. ( _I |` ( NN \ { 1 } ) ) ) ) $.
    $( Lemma for ~ acdc5 . $)
    acdc5lem1 $p |- ( ( ( r We A /\ F : ( NN X. A ) --> ( ~P A \ { (/) } ) )
                  /\ ( X e. A /\ K e. NN ) ) ->
             ( ( X S K ) e. ( K F X ) /\ ( X S K ) e. A ) ) $=
      ( cv wwe cn cxp cpw c0 csn cdif wf wa wcel co wbr wn wral crab cuni wceq
      oprex rabex uniex opreq2 rabeq raleq rabbidv eqtrd syl unieqd opreq1
      oprabval2 adantl wss wne w3a foprrn eldifsn sylib elpwi anim1i 3com23
      3expb wereucl sylan2 anassrs eqeltrd simpld adantll sseldd jca ) FLQZRZSF
      TFUAZUBUCUDZHUEZUFZKFUGZJSUGZUFZUFZKJGUHZJKHUHZUGWPFUGWOWPEQDQWFUIUJZEWQU
      KZDWQULZUMZWQWNWPXAUNWKABCKJFSWREBQZAQZHUHZUKZDXDULZUMXAGWREXBKHUHZUKZDXG
      ULZUMWTWSDWQJKHUOZUPUQXCKUNZXFXIXKXDXGUNZXFXIUNXCKXBHURXLXFXEDXGULXIXEDXD
      XGUSXLXEXHDXGWREXDXGUTVAVBVCVDXBJUNZXIWTXMXGWQUNZXIWTUNXBJKHVEXNXIXHDWQUL
      WTXHDXGWQUSXNXHWSDWQWREXGWQUTVAVBVCVDOVFVGWGWJWNXAWQUGZWJWNUFZWGWQFVHZWQU
      BVIZUFZXOWJWLWMXSWJWMWLXSWJWMWLVJZWQWHUGZXRUFZXSXTWQWIUGYBJKWISFHVKWQWHUB
      VLVMYAXQXRWQFVNVOVCVPVQZWGXQXRXODEFWQWFXJVRVQVSVTWAZWOWQFWPWJWNXQWGXPXQXR
      YCWBWCYDWDWE $.
      $( [30-Sep-2007] $)

    $( Lemma for ~ acdc5 .  Build a sequence ` G ` starting at value ` c ` , as
       follows.  Given a previous value ` x ` of ` G ` , we construct, for the
       next value of ` G ` , the ` v ` such that
       ` A. u e. ( y F x ) -. u r v ` , which is unique when ` r ` is a
       well-ordering on ` A ` . $)
    acdc5lem2 $p |- ( ( r We A /\ ( c e. A /\
     F : ( NN X. A ) --> ( ~P A \ { (/) } ) ) ) -> ( G : NN --> A /\
       ( G ` 1 ) = c /\
               A. k e. NN ( G ` ( k + 1 ) ) e. ( ( k + 1 ) F ( G ` k ) ) ) ) $=
      ( vs vt cv wwe wcel cn cxp cpw c0 csn cdif wf wa c1 cfv wceq caddc co
      wral cop cid cres cun cseq1 1nn elisseti visset eqid fvsnun1 id syl5eqel
      ad2antrl wss wf1o f1oi f1of ax-mp difss fss mp2an resundir cin disjdif
      wfn wb fnsn fnresdisj mpbi residm uneq12i uncom un0 eqtr3i 3eqtri feq1i
      mpbir jctir acdc5lem1 simprd ex r19.21aivv wbr wn crab cuni oprex rabex
      uniex fnoprab2 jctil ffnoprv sylibr adantrl nnex oprabex2 snex cvv
      difexg resiexg unex seq1f2 3expa syl11anc fveq1i seq11 a1i seq1p1
      seq1lem2 fvsnun2 syl fvi syl6eq opreq2d eqtrd opreq1i 3eqtr4g adantl
      simpll simplrr simpr simplrl adantr w3a simp2 simp3 simp1 seq1cl2
      syl31anc syl111anc peano2nn simpld syl22anc eqeltrd r19.21aiva 3jca ) FKR
      ZSZLRZFTZUAFUBFUCUDUEUFIUGZUHZUHZUAFJUGZUIJUJZUUMUKZHRZUIULUMZJUJZUVBUVAJ
      UJZIUMZTZHUAUNUUQUAFGUIUUMUOZUEZUPUAUIUEZUFZUQZURZUSUMZUGZUURUUQUIUVLUJZF
      TZUVJUAUVLUVJUQZUGZUHFUAUBZFGUGZUVNUUQUVPUVRUUNUVPUULUUOUUNUVOUUMFUIUUMUA
      UPUVLUIUAUTVAZLVBZUVLVCZVDZUUNVEVFVGUVRUVJUAUVKUGZUVJUVJUVKUGZUVJUAVHUWEU
      VJUVJUVKVIUWFUVJVJUVJUVJUVKVKVLUAUVIVMUVJUVJUAUVKVNVOUVJUAUVQUVKUVQUVHUVJ
      UQZUVKUVJUQZURUDUVKURZUVKUVHUVKUVJVPUWGUDUWHUVKUVIUVJVQUDUKZUWGUDUKZUVIUA
      VRUVHUVIVSUWJUWKVTUIUUMUWAWAUVIUVJUVHWBVLWCUPUVJWDWEUVKUDURUWIUVKUVKUDWFU
      VKWGWHWIWJWKZWLUULUUOUVTUUNUULUUOUHZGUVSVSZPRZQRZGUMZFTZQUAUNPFUNZUHUVTUW
      MUWSUWNUWMUWRPQFUAUWMUWOFTUWPUATUHZUWRUWMUWTUHUWQUWPUWOIUMTUWRABCDEFGIJUW
      PUWOKLMNOWMWNWOWPABCFUAERDRUUKWQWREBRZARZIUMZUNZDUXCWSZWTZGUXEUXDDUXCUXAU
      XBIXAXBXCNXDXEPQFUAFGXFXGXHZUVPUVRUVTUVNFUAGUVLABCFUAUXFGMXINXJZUVHUVKUVG
      XKUVJXLTZUVKXLTUAXLTUXIXIUAUVIXLXMVLUVJXLXNVLXOZXPXQXRUAFJUVMOWJXGUUTUUQU
      USUIUVMUJUVOUUMUIJUVMOXSGUVLUXHUXJXTUWDWIYAUUQUVFHUAUUQUVAUATZUHZUVCUVDUV
      BGUMZUVEUXKUVCUXMUKUUQUXKUVBUVMUJZUVAUVMUJZUVBGUMZUVCUXMUXKUXNUXOUVBUVLUJ
      ZGUMUXPUVAGUVLUXHUXJYBUXKUXQUVBUXOGUXKUXQUVBUPUJZUVBUXKUVBUVJTUXQUXRUKUVA
      YCUIUUMUAUVBUPUVLUWAUWBUWCYDYEUVBXLTUXRUVBUKUVAUIULXAUVBXLYFVLYGYHYIUVBJU
      VMOXSUVDUXOUVBGUVAJUVMOXSZYJYKYLUXLUULUUOUVDFTZUVBUATZUXMUVETZUULUUPUXKYM
      UULUUNUUOUXKYNUXLUVDUXOFUXSUXLUXKUUNUVTUXOFTZUUQUXKYOUULUUNUUOUXKYPUUQUVT
      UXKUXGYQUXKUUNUVTYRZUVPUVRUVTUXKUYCUYDUVOUUMFUWDUXKUUNUVTYSVFUVRUYDUWLYAU
      XKUUNUVTYTUXKUUNUVTUUAUVAFUAGUVLUXHUXJUUBUUCUUDVFUXKUYAUUQUVAUUEYLUWMUXTU
      YAUHUHUYBUXMFTABCDEFGIJUVBUVDKLMNOWMUUFUUGUUHUUIUUJ $.
      $( [9-Oct-2007] $)
  $}

  ${
    $d a b c d g k r u v x y z A $.  $d a b c d f g h k r u v x y z F $.
    $d c g C $.
    acdc5.1 $e |- A e. _V $.
    $( A more general version of ~ acdc that has an initial value and where the
       function ` F ` depends on ` k ` . $)
    acdc5 $p |- ( ( F : ( NN X. A ) --> ( ~P A \ { (/) } ) /\ C e. A ) ->
               E. g ( g : NN --> A /\ ( g ` 1 ) = C /\
           A. k e. NN ( g ` ( k + 1 ) ) e. ( ( k + 1 ) F ( g ` k ) ) ) ) $=
      ( vc vr va vb vd vh vf vx vy vz vv vu wcel cn cxp cpw c0 csn cdif wf cv
      c1 cfv wceq caddc co wral w3a wex wi eqeq2 3anbi2d exbidv imbi2d wwe cvv
      weth ax-mp wa wbr wn crab cuni copab2 cop cid cres cun cseq1 eleq1
      bi2anan9 opreq12 ancoms rabeq raleq rabbidv eqtrd syl breq2 notbid
      ralbidv breq1 cbvralv syl6bb cbvrabv syl6eq unieqd eqeq2d anbi12d
      cbvoprab12v eqeq1 anbi2d cbvoprab3v eqtri eqid acdc5lem2 oprex feq1
      fveq1 eqeq1d opreq2d eleq12d 3anbi123d cla4ev exp32 19.23aiv vtoclga
      impcom ) BASTAUAAUBUCUDUEEUFZTACUGZUFZUHXPUIZBUJZDUGZUHUKULZXPUIZYAXTXPUI
      ZEULZSZDTUMZUNZCUOZXOXQXRGUGZUJZYFUNZCUOZUPZXOYHUPGBAYIBUJZYLYHXOYNYKYGCY
      NYJXSXQYFYIBXRUQURUSUTAHUGZVAZHUOZYIASZYMUPZAVBSYQFHAVBVCVDYPYSHYPYRXOYLY
      PYRXOVEVETAIUGZASZJUGZTSZVEZKUGZLUGZMUGZYOVFZVGZLUUBYTEULZUMZMUUJVHZVIZUJ
      ZVEZIJKVJZUHYIVKUDVLTUHUDUEVMVNZVOULZUFZUHUURUIZYIUJZYAUURUIZYAXTUURUIZEU
      LZSZDTUMZUNZYLNOPQRAUUPDEUURHGFUUPNUGZASZOUGZTSZVEZUUERUGZQUGZYOVFZVGZRUV
      JUVHEULZUMZQUVQVHZVIZUJZVEZNOKVJUVLPUGZUVTUJZVEZNOPVJUUOUWBIJKNOYTUVHUJZU
      UBUVJUJZVEZUUDUVLUUNUWAUWFUUAUVIUWGUUCUVKYTUVHAVPUUBUVJTVPVQUWHUUMUVTUUEU
      WHUULUVSUWHUULUUILUVQUMZMUVQVHZUVSUWHUUJUVQUJZUULUWJUJUWGUWFUWKUUBUVJYTUV
      HEVRVSUWKUULUUKMUVQVHUWJUUKMUUJUVQVTUWKUUKUWIMUVQUUILUUJUVQWAWBWCWDUWIUVR
      MQUVQUUGUVNUJZUWIUUFUVNYOVFZVGZLUVQUMUVRUWLUUIUWNLUVQUWLUUHUWMUUGUVNUUFYO
      WEWFWGUWNUVPLRUVQUUFUVMUJUWMUVOUUFUVMUVNYOWHWFWIWJWKWLWMWNWOWPUWBUWENOKPU
      UEUWCUJUWAUWDUVLUUEUWCUVTWQWRWSWTUURXAXBYKUVGCUURUUPUUQVOXCXPUURUJZXQUUSY
      JUVAYFUVFTAXPUURXDUWOXRUUTYIUHXPUURXEXFUWOYEUVEDTUWOYBUVBYDUVDYAXPUURXEUW
      OYCUVCYAEXTXPUURXEXGXHWGXIXJWDXKXLVDXMXN $.
      $( [11-Oct-2007] $)
  $}

  ${
    $d k s t u v x y z A $.  $d k s t u v x y z F $.  $d u v x y z G $.
    $d s t S $.  $d k s t x y z c $.  $d k s t u v x y z r $.
    acdclem.1 $e |- A e. _V $.
    acdclem.2 $e |- S = { <. <. x , y >. , z >. |
               ( ( x e. A /\ y e. A ) /\ z = U. { v e. ( F ` x ) |
                        A. u e. ( F ` x ) -. u r v } ) } $.
    acdclem.3 $e |- G = ( S seq1 ( NN X. { c } ) ) $.
    $( Lemma for ~ acdc .  Build a sequence ` G ` starting at value ` c ` , as
       follows.  Given a previous value ` x ` of ` G ` , we construct, for the
       next value of ` G ` , the ` v ` such that
       ` A. u e. ( F `` x ) -. u r v ` , which is unique when ` r ` is a
       well-ordering on ` A ` . $)
    acdclem $p |- ( ( r We A /\ ( c e. A /\ F : A --> ( ~P A \ { (/) } ) ) )
  -> ( G : NN --> A /\ A. k e. NN ( G ` ( k + 1 ) ) e. ( F ` ( G ` k ) ) ) ) $=
      ( vs vt cv wwe wcel cpw c0 csn cdif wf wa cn c1 caddc co cfv wral cxp
      cseq1 wss visset fconst snssi fss sylancr ad2antrl wfn wbr wn crab cuni
      wceq fvex rabex uniex fveq2 rabeq raleq rabbidv eqtrd syl unieqd eqidd
      oprabval2 adantl ffvelrn eldifi adantll elpwi wne simpll eldifn id 0ex
      snid syl6eqel necon3bi wereucl syl111anc sseldd adantlrl adantrr eqeltrd
      ex r19.21aivv fnoprab2 jctil ffnoprv sylibr oprabex2 nnex snex xpex
      seq1f syl11anc feq1i fveq1i seq1p1 opreq1i peano2nn fvconst2 opreq2d
      simpr adantr ad2antlr seq1cl syl5eqel simplrl syl5eqr syl5eq simplrr
      r19.21aiva jca ) FKRZSZLRZFTZFFUAZUBUCZUDZIUEZUFZUFZUGFJUEZHRZUHUIUJZJUKZ
      YTJUKZIUKZTZHUGULYRUGFGUGYKUCZUMZUNUJZUEZYSYRUGFUUGUEZFFUMZFGUEZUUIYLUUJY
      JYPYLUGUUFUUGUEUUFFUOUUJUGYKLUPZUQYKFURUGUUFFUUGUSUTZVAYRGUUKVBZPRZQRZGUJ
      ZFTZQFULPFULZUFUULYRUUTUUOYRUUSPQFFYRUUPFTZUUQFTZUFZUUSYRUVCUFUURERDRYIVC
      VDZEUUPIUKZULZDUVEVEZVFZFUVCUURUVHVGYRABCUUPUUQFFUVDEARZIUKZULZDUVJVEZVFZ
      UVHGUVHUVGUVFDUVEUUPIVHZVIVJUVIUUPVGZUVLUVGUVOUVJUVEVGZUVLUVGVGUVIUUPIVKU
      VPUVLUVKDUVEVEUVGUVKDUVJUVEVLUVPUVKUVFDUVEUVDEUVJUVEVMVNVOVPVQBRZUUQVGUVH
      VRNVSVTYRUVAUVHFTZUVBYJYPUVAUVRYLYJYPUFUVAUFZUVEFUVHUVSUVEYMTZUVEFUOZYPUV
      AUVTYJYPUVAUFZUVEYOTZUVTFYOUUPIWAZUVEYMYNWBVPWCUVEFWDVPZUVSYJUWAUVEUBWEZU
      VHUVETYJYPUVAWFUWEYPUVAUWFYJUWBUWCUWFUWDUWCUVEYNTZVDUWFUVEYMYNWGUWGUVEUBU
      VEUBVGZUVEUBYNUWHWHUBWIWJZWKWLVPVPWCDEFUVEYIUVNWMWNWOWPWQWRWSWTABCFFUVMGU
      VLUVKDUVJUVIIVHVIVJNXAXBPQFFFGXCXDZFGUUGABCFFUVMGMMNXEZUGUUFXFYKXGXHZXIXJ
      UGFJUUHOXKXDYRUUEHUGYRYTUGTZUFZUUBUVDEUUDULZDUUDVEZVFZUUDUWNUUBUUAUUHUKZU
      WQUUAJUUHOXLUWNUWRYTUUHUKZUUAUUGUKZGUJZUWQUWMUWRUXAVGYRYTGUUGUWKUWLXMVTUW
      NUXAUUCUWTGUJZUWQUUCUWSUWTGYTJUUHOXLZXNUWNUXBUUCYKGUJZUWQUWMUXBUXDVGYRUWM
      UWTYKUUCGUWMUUAUGTUWTYKVGYTXOUGYKUUAUUMXPVPXQVTUWNUUCFTZYLUXDUWQVGUWNUUCU
      WSFUXCUWNUWMUUJUULUWSFTYRUWMXRYQUUJYJUWMYLUUJYPUUNXSXTYRUULUWMUWJXSYTFGUU
      GUWKUWLYAWNYBZYJYLYPUWMYCABCUUCYKFFUVMUWQGUWQUWPUWODUUDUUCIVHZVIVJUVIUUCV
      GZUVLUWPUXHUVJUUDVGZUVLUWPVGUVIUUCIVKUXIUVLUVKDUUDVEUWPUVKDUVJUUDVLUXIUVK
      UWODUUDUVDEUVJUUDVMVNVOVPVQUVQYKVGUWQVRNVSXJVOYDVOYEUWNYJUUDFUOZUUDUBWEZU
      WQUUDTYJYQUWMWFUWNUUDYMTZUXJUWNUUDYOTZUXLUWNYPUXEUXMYJYLYPUWMYFUXFFYOUUCI
      WAXJZUUDYMYNWBVPUUDFWDVPUWNUXMUXKUXNUXMUUDYNTZVDUXKUUDYMYNWGUXOUUDUBUUDUB
      VGZUUDUBYNUXPWHUWIWKWLVPVPDEFUUDYIUXGWMWNWRYGYH $.
      $( [21-Sep-2007] $)
  $}

  ${
    $d a b c d g k r u v x y z A $.  $d a b c d f g h k r u v x y z F $.
    acdc.1 $e |- A e. _V $.
    $( Dependent Choice.  Axiom DC1 of [Schechter] p. 149.  This theorem is
       weaker than the Axiom of Choice but is stronger than Countable Choice.
       It shows the existence of a sequence whose values can only be shown to
       exist (but cannot be constructed explicitly) and also depend on earlier
       values in the sequence. $)
    acdc $p |- ( ( A =/= (/) /\ F : A --> ( ~P A \ { (/) } ) ) -> E. g
     ( g : NN --> A /\ A. k e. NN ( g ` ( k + 1 ) ) e. ( F ` ( g ` k ) ) ) ) $=
      ( vr vc va vb vd vh vf vx vy vz vv vu cv wwe wex c0 wne cpw csn cdif wf
      wa cn c1 caddc co cfv wcel wral wi cvv weth ax-mp n0 wbr wn crab cuni
      wceq copab2 cxp cseq1 eleq1 bi2anan9 fveq2 rabeq raleq rabbidv eqtrd syl
      adantr breq2 notbid ralbidv breq1 cbvralv syl6bb cbvrabv syl6eq unieqd
      eqeq2d anbi12d cbvoprab12v eqeq1 anbi2d cbvoprab3v eqtri eqid acdclem
      oprex feq1 fveq1 fveq2d eleq12d cla4ev exp32 19.23adv syl5bi imp3a
      19.23aiv ) AFRZSZFTZAUAUBZAAUCUAUDUEDUFZUGUHABRZUFZCRZUIUJUKZXKULZXMXKULZ
      DULZUMZCUHUNZUGZBTZUOZAUPUMXHEFAUPUQURXGYBFXGXIXJYAXIGRZAUMZGTXGXJYAUOZGA
      USXGYDYEGXGYDXJYAXGYDXJUGUGUHAHRZAUMZIRZAUMZUGZJRZKRZLRZXFUTZVAZKYFDULZUN
      ZLYPVBZVCZVDZUGZHIJVEZUHYCUDVFZVGUKZUFZXNUUDULZXMUUDULZDULZUMZCUHUNZUGZYA
      MNOPQAUUBCDUUDFGEUUBMRZAUMZNRZAUMZUGZYKQRZPRZXFUTZVAZQUULDULZUNZPUVAVBZVC
      ZVDZUGZMNJVEUUPORZUVDVDZUGZMNOVEUUAUVFHIJMNYFUULVDZYHUUNVDZUGZYJUUPYTUVEU
      VJYGUUMUVKYIUUOYFUULAVHYHUUNAVHVIUVLYSUVDYKUVLYRUVCUVLYRYOKUVAUNZLUVAVBZU
      VCUVJYRUVNVDZUVKUVJYPUVAVDZUVOYFUULDVJUVPYRYQLUVAVBUVNYQLYPUVAVKUVPYQUVML
      UVAYOKYPUVAVLVMVNVOVPUVMUVBLPUVAYMUURVDZUVMYLUURXFUTZVAZKUVAUNUVBUVQYOUVS
      KUVAUVQYNUVRYMUURYLXFVQVRVSUVSUUTKQUVAYLUUQVDUVRUUSYLUUQUURXFVTVRWAWBWCWD
      WEWFWGWHUVFUVIMNJOYKUVGVDUVEUVHUUPYKUVGUVDWIWJWKWLUUDWMWNXTUUKBUUDUUBUUCV
      GWOXKUUDVDZXLUUEXSUUJUHAXKUUDWPUVTXRUUICUHUVTXOUUFXQUUHXNXKUUDWQUVTXPUUGD
      XMXKUUDWQWRWSVSWGWTVOXAXBXCXDXEUR $.
      $( [21-Sep-2007] $)
  $}

  ${
    $d g k x y z A $.  $d g k x y z F $.
    acdcALT.1 $e |- A e. _V $.
    $( Dependent Choice.  Axiom DC1 of [Schechter] p. 149.  This theorem is
       weaker than the Axiom of Choice but is stronger than Countable Choice.
       It shows the existence of a sequence whose values can only be shown to
       exist (but cannot be constructed explicitly) and also depend on earlier
       values in the sequence. $)
    acdcALT $p |- ( ( A =/= (/) /\ F : A --> ( ~P A \ { (/) } ) ) -> E. g
     ( g : NN --> A /\ A. k e. NN ( g ` ( k + 1 ) ) e. ( F ` ( g ` k ) ) ) ) $=
      ( vx vy vz c0 wne cpw csn cdif wf wa cn cv c1 caddc co cfv wcel wceq
      copab2 wral wex cxp ffvelrn ex adantld r19.21aivv eqid foprab2 sylib
      acdc2 sylan2 peano2nn adantl fvex eqidd fveq2 oprabval2 syl11anc eleq2d
      ralbidva pm5.32i exbii ) AIJZAAKILMZDNZOPABQZNZCQZRSTZVKUAZVNVMVKUAZFQZPU
      BZGQZAUBZOHQVSDUAZUCOFGHUDZTZUBZCPUEZOZBUFZVLVOVPDUAZUBZCPUEZOZBUFVJVHPAU
      GVIWBNZWGVJWAVIUBZGAUEFPUEWLVJWMFGPAVJVTWMVRVJVTWMAVIVSDUHUIUJUKFGHPAWAVI
      WBWBULZUMUNABCWBEUOUPWFWKBVLWEWJVLWDWICPVLVMPUBZOZWCWHVOWPVNPUBZVPAUBWCWH
      UCWOWQVLVMUQURPAVMVKUHFGHVNVPPAWAWHWBWAVPDUSVQVNUCWAUTVSVPDVAWNVBVCVDVEVF
      VGUN $.
      $( [1-Oct-2007] $)
  $}

