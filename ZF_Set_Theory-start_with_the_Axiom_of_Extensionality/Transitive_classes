$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Transitive classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare a new symbol. $)
  $c Tr $. $( Transitive predicate (read:  "the following class is
              transitive") $)

  $( Extend wff notation to include transitive classes.  Notation from
     [TakeutiZaring] p. 35. $)
  wtr $a wff Tr A $.

  $( Define the transitive class predicate.  Not to be confused with a
     transitive relation (see ~ cotr ).  Definition of [Enderton] p. 71
     extended to arbitrary classes.  For alternate definitions, see ~ dftr2
     (which is suggestive of the word "transitive"), ~ dftr3 , ~ dftr4 ,
     ~ dftr5 , and (when ` A ` is a set) ~ unisuc .  The term "complete" is
     used instead of "transitive" in Definition 3 of [Suppes] p. 130. $)
  df-tr $a |- ( Tr A <-> U. A C_ A ) $.

  ${
    $d x y A $.
    $( An alternate way of defining a transitive class.  Exercise 7 of
       [TakeutiZaring] p. 40. $)
    dftr2 $p |- ( Tr A <-> A. x A. y ( ( x e. y /\ y e. A ) -> x e. A ) ) $=
      ( cuni wss cv wcel wi wal wtr wa dfss2 df-tr wex 19.23v eluni imbi1i
      bitr4i albii 3bitr4i ) CDZCEAFZUAGZUBCGZHZAICJUBBFZGUFCGKZUDHBIZAIAUACLCM
      UHUEAUHUGBNZUDHUEUGUDBOUCUIUDBUBCPQRST $.
      $( [24-Apr-1994] $)

    $( An alternate way of defining a transitive class. $)
    dftr5 $p |- ( Tr A <-> A. x e. A A. y e. x y e. A ) $=
      ( wtr cv wcel wa wi wal wral dftr2 alcom impexp albii df-ral r19.21v
      3bitr2i bitr4i 3bitri ) CDBEZAEZFZUACFZGTCFZHZAIBIUEBIZAIZUDBUAJZACJZBACK
      UEBALUGUCUHHZAIUIUFUJAUFUBUCUDHZHZBIUKBUAJUJUEULBUBUCUDMNUKBUAOUCUDBUAPQN
      UHACORS $.
      $( [20-Mar-2004] $)

    $( An alternate way of defining a transitive class.  Definition 7.1 of
       [TakeutiZaring] p. 35. $)
    dftr3 $p |- ( Tr A <-> A. x e. A x C_ A ) $=
      ( vy wtr cv wcel wral wss dftr5 dfss3 ralbii bitr4i ) BDCEBFCAEZGZABGMBHZ
      ABGACBIONABCMBJKL $.
      $( [29-Aug-1993] $)
  $}

  $( An alternate way of defining a transitive class.  Definition of [Enderton]
     p. 71. $)
  dftr4 $p |- ( Tr A <-> A C_ ~P A ) $=
    ( wtr cuni wss cpw df-tr sspwuni bitr4i ) ABACADAAEDAFAAGH $.
    $( [8-May-2009] $) $( [29-Aug-1993] $)

  $( Equality theorem for the transitive class predicate. $)
  treq $p |- ( A = B -> ( Tr A <-> Tr B ) ) $=
    ( wceq cuni wss wtr unieq sseq1d sseq2 bitrd df-tr 3bitr4g ) ABCZADZAEZBDZB
    EZAFBFMOPAEQMNPAABGHABPIJAKBKL $.
    $( [17-Sep-1993] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( In a transitive class, the membership relation is transitive.  (The
       proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    trel $p |- ( Tr A -> ( ( B e. C /\ C e. A ) -> B e. A ) ) $=
      ( vy vx wtr cv wcel wa wi wal dftr2 wceq eleq12 wb eleq1 adantl anbi12d
      adantr imbi12d cla42gv pm2.43b sylbi ) AFDGZEGZHZUEAHZIZUDAHZJZEKDKZBCHZC
      AHZIZBAHZJZDEALUKUNUOUJUPDEBCCAUDBMZUECMZIZUHUNUIUOUSUFULUGUMUDBUECNURUGU
      MOUQUECAPQRUQUIUOOURUDBAPSTUAUBUC $.
      $( [9-Jul-2011] $) $( [19-Apr-1994] $)
  $}

  $( In a transitive class, the membership relation is transitive. $)
  trel3 $p |- ( Tr A -> ( ( B e. C /\ C e. D /\ D e. A ) -> B e. A ) ) $=
    ( wtr wcel w3a wa 3anass trel anim2d syl5bi syld ) AEZBCFZCDFZDAFZGZOCAFZHZ
    BAFROPQHZHNTOPQINUASOACDJKLABCJM $.
    $( [19-Apr-1994] $)

  ${
    $d x A $.  $d x B $.
    $( An element of a transitive class is a subset of the class. $)
    trss $p |- ( Tr A -> ( B e. A -> B C_ A ) ) $=
      ( vx wtr wcel wss cv wi wceq eleq1 sseq1 imbi12d imbi2d wral dftr3 ra4
      sylbi vtoclg pm2.43b ) ADZBAEZBAFZTCGZAEZUCAFZHZHTUAUBHZHCBAUCBIZUFUGTUHU
      DUAUEUBUCBAJUCBAKLMTUECANUFCAOUECAPQRS $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The intersection of transitive classes is transitive. $)
    trin $p |- ( ( Tr A /\ Tr B ) -> Tr ( A i^i B ) ) $=
      ( vx wtr wa cv cin wss wral wcel elin trss im2anan9 syl5bi ssin syl6ib
      r19.21aiv dftr3 sylibr ) ADZBDZEZCFZABGZHZCUDIUDDUBUECUDUBUCUDJZUCAHZUCBH
      ZEZUEUFUCAJZUCBJZEUBUIUCABKTUJUGUAUKUHAUCLBUCLMNUCABOPQCUDRS $.
      $( [9-May-1994] $)
  $}

  $( The empty set is transitive. $)
  tr0 $p |- Tr (/) $=
    ( c0 wtr cpw wss 0ss dftr4 mpbir ) ABAACZDHEAFG $.
    $( [16-Sep-1993] $)

  $( The universe is transitive. $)
  trv $p |- Tr _V $=
    ( cvv wtr cuni wss ssv df-tr mpbir ) ABACZADHEAFG $.
    $( [14-Sep-2003] $)

  ${
    $d x y A $.
    $( The union of a class of transitive sets is transitive.  Exercise 5(a) of
       [Enderton] p. 73.  (Contributed by Scott Fenton, 21-Feb-2011.) $)
    truni $p |- ( A. x e. A Tr x -> Tr U. A ) $=
      ( vy cv wtr wral wel cuni wss wi wal wcel wa dftr3 biimpi a1i ax-17
      alral syl jctird r19.26 syl6ibr ssuni ralimi syl6 ralimia df-ral ralbii
      ralcom4 bitr2i sylibr wrex eluni2 imbi1i r19.23v bitr4i albii 3bitri ) AD
      ZEZABFZCAGZCDZBHZIZJZABFZCKZVDEZVAVECUSFZABFZVHUTVJABUSBLZUTVCUSIZVLMZCUS
      FZVJVLUTVMCUSFZVLCUSFZMVOVLUTVPVQUTVPJVLUTVPCUSNOPVLVLCKVQVLCQVLCUSRSTVMV
      LCUSUAUBVNVECUSVCUSBUCUDUEUFVKVFCKZABFVHVJVRABVECUSUGUHVFACBUIUJUKVIVECVD
      FVCVDLZVEJZCKVHCVDNVECVDUGVTVGCVTVBABULZVEJVGVSWAVEAVCBUMUNVBVEABUOUPUQUR
      UK $.
      $( [21-Feb-2011] $)

    $( The intersection of a class of transitive sets is transitive.  Exercise
       5(b) of [Enderton] p. 73.  (Contributed by Scott Fenton,
       25-Feb-2011.) $)
    trint $p |- ( A. x e. A Tr x -> Tr |^| A ) $=
      ( vy cv wtr wral wel wss wi wal cint dftr3 ralbii biimpi df-ral ralcom4
      bitri sylib ralim alimi syl wcel visset elint2 ssint imbi12i albii
      3bitri sylibr ) ADZEZABFZCAGZABFZCDZUJHZABFZIZCJZBKZEZULUMUPIZABFZCJZUSUL
      UPCUJFZABFZVDULVFUKVEABCUJLMNVFVBCJZABFVDVEVGABUPCUJOMVBACBPQRVCURCUMUPAB
      STUAVAUOUTHZCUTFUOUTUBZVHIZCJUSCUTLVHCUTOVJURCVIUNVHUQAUOBCUCUDAUOBUEUFUG
      UHUI $.
      $( [25-Feb-2011] $)

    $( If ` A ` is transitive and non-null, then ` |^| A ` is a subset of
       ` A ` .  (Contributed by Scott Fenton, 3-Mar-2011.) $)
    trintss $p |- ( ( A =/= (/) /\ Tr A ) -> |^| A C_ A ) $=
      ( vy vx c0 wne wtr wa cint cv wcel wel wral visset elint2 wrex r19.2z ex
      trel ancomsd exp3a r19.23adv sylan9 syl5bi ssrdv ) ADEZAFZGZBAHZABIZUHJBC
      KZCALZUGUIAJZCUIABMNUEUKUJCAOZUFULUEUKUMUJCAPQUFUJULCAUFCIZAJZUJULUFUJUOU
      LAUIUNRSTUAUBUCUD $.
      $( [3-Mar-2011] $)
  $}

