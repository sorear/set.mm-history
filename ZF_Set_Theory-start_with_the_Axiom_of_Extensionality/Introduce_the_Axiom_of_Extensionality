$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Introduce the Axiom of Extensionality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $v t $.
  $( Let ` t ` be an individual variable. $)
  vt $f set t $.

$(
$)

  ${
    $d x y z $.
    $( Axiom of Extensionality.  An axiom of Zermelo-Fraenkel set theory.  It
       states that two sets are identical if they contain the same elements.
       Axiom Ext of [BellMachover] p. 461.

       Set theory can also be formulated with a _single_ primitive predicate
       ` e. ` on top of traditional predicate calculus _without_ equality.  In
       that case the Axiom of Extensionality becomes
       ` ( A. w ( w e. x <-> w e. y ) -> ( x e. z -> y e. z ) ) ` , and
       equality ` x = y ` is _defined_ as ` A. w ( w e. x <-> w e. y ) ` .  All
       of the usual axioms of equality then become theorems of set theory.
       See, for example, Axiom 1 of [TakeutiZaring] p. 8.

       To use the above "equality-free" version of Extensionality with
       Metamath's logical axioms, we would rewrite ~ ax-8 through ~ ax-16 with
       equality expanded according to the above definition.  Some of those
       axioms could be proved from set theory and would be redundant.  Not all
       of them are redundant, since our axioms of predicate calculus make
       essential use of equality for the proper substitution that is a
       primitive notion in traditional predicate calculus.  A study of such an
       axiomatization would be an interesting project for someone exploring the
       foundations of logic.

       _General remarks_:  Our set theory axioms are presented using defined
       connectives ( ` <-> ` , ` E. ` , etc.) for convenience.  However, it is
       implicitly understood that the actual axioms use only the primitive
       connectives ` -> ` , ` -. ` , ` A. ` , ` = ` , and ` e. ` .  It is
       straightforward to establish the equivalence between the actual axioms
       and the ones we display, and we will not do so.

       It is important to understand that strictly speaking, all of our set
       theory axioms are really schemes that represent an infinite number of
       actual axioms.  This is inherent in the design of Metamath
       ("metavariable math"), which manipulates only metavariables.  For
       example, the metavariable ` x ` in ~ ax-ext can represent any actual
       variable _v1_, _v2_, _v3_,... .  Distinct variable restrictions ($d)
       prevent us from substituting say _v1_ for both ` x ` and ` z ` .  This
       is in contrast to typical textbook presentations that present actual
       axioms (except for Replacement ~ ax-rep , which involves a wff
       metavariable).  In practice, though, the theorems and proofs are
       essentially the same.  The $d restrictions make each of the the infinite
       axioms generated by the ~ ax-ext scheme exactly logically equivalent to
       each other and in particular to the actual axiom of the textbook
       version. $)
    ax-ext $a |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $.

    $( The Axiom of Extensionality ( ~ ax-ext ) restated so that it postulates
       the existence of a set ` z ` given two arbitrary sets ` x ` and ` y ` .
       This way to express it follows the general idea of the other ZFC axioms,
       which is to postulate the existence of sets given other sets. $)
    axext2 $p |- E. z ( ( z e. x <-> z e. y ) -> x = y ) $=
      ( cv wcel wb wceq wi wex wal ax-ext 19.36v mpbir ) CDZADZENBDZEFZOPGZHCIQ
      CJRHABCKQRCLM $.
      $( [28-Sep-2003] $)
  $}

  ${
    $d z x w $.  $d z y w $.
    $( A generalization of the Axiom of Extensionality in which ` x ` and ` y `
       need not be distinct.  (The proof was shortened by Andrew Salmon,
       12-Aug-2011.) $)
    axext3 $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( vw cv wcel wb wal wceq wi elequ2 bibi1d albidv equequ1 imbi12d ax-ext
      chvarv ) CEZDEZFZRBEZFZGZCHZSUAIZJRAEZFZUBGZCHZUFUAIZJDASUFIZUDUIUEUJUKUC
      UHCUKTUGUBDACKLMDABNODBCPQ $.
      $( [12-Aug-2011] $) $( [15-Sep-1993] $)

    $( A generalization of the Axiom of Extensionality in which ` x ` and ` y `
       need not be distinct. $)
    axext3OLD $p |- ( A. z ( z e. x <-> z e. y ) -> x = y ) $=
      ( vw cv wceq wex wcel wb wal wi a9e ax-ext elequ2 bibi1d albidv equequ1
      imbi12d mpbii 19.23aiv ax-mp ) DEZAEZFZDGCEZUCHZUEBEZHZIZCJZUCUGFZKZDALUD
      ULDUDUEUBHZUHIZCJZUBUGFZKULDBCMUDUOUJUPUKUDUNUICUDUMUFUHDACNOPDABQRSTUA
      $.
      $( [15-Sep-1993] $)

    $( A bidirectional version of Extensionality.  Although this theorem
       "looks" like it is just a definition of equality, it requires the Axiom
       of Extensionality for its proof under our axiomatization.  See the
       comments for ~ ax-ext and ~ df-cleq . $)
    axext4 $p |- ( x = y <-> A. z ( z e. x <-> z e. y ) ) $=
      ( cv wceq wcel wb wal elequ2 19.21aiv axext3 impbii ) ADZBDZEZCDZMFPNFGZC
      HOQCABCIJABCKL $.
      $( [14-Nov-2008] $) $( [14-Nov-2008] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    bm1.1.1 $e |- ( ph -> A. x ph ) $.
    $( Any set defined by a property is the only set defined by that property.
       Theorem 1.1 of [BellMachover] p. 462. $)
    bm1.1 $p |- ( E. x A. y ( y e. x <-> ph ) ->
                  E! x A. y ( y e. x <-> ph ) ) $=
      ( vz cv wcel wb wal wex wsbc wa wceq wi weu 19.26 biantr 19.20i ax-ext
      syl sylbir ax-17 hbbi hbal elequ2 bibi1d albidv sbie sylan2b gen2 jctr
      eu2 sylibr ) CFZBFZGZAHZCIZBJZUSURURBEFZKZLUOUTMZNZEIBIZLURBOUSVDVCBEURUN
      UTGZAHZCIZVBVAURVGLUQVFLZCIZVBUQVFCPVIUPVEHZCIVBVHVJCUPAVEQRBECSTUAURVGBE
      VFBCVEABVEBUBDUCUDVBUQVFCVBUPVEABECUEUFUGUHUIUJUKURBEUREUBULUM $.
      $( [30-Jun-1994] $)
  $}


