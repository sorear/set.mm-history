$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Binary relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend wff notation to include the general binary relation predicate.
     Note that the syntax is simply three class symbols in a row.  Since binary
     relations are the only possible wff expressions consisting of three class
     expressions in a row, the syntax is unambiguous.  (For an example of how
     syntax could become ambiguous if we are not careful, see the comment in
     ~ cneg .) $)
  wbr $a wff A R B $.

  $( Define a general binary relation.  Note that the syntax is simply three
     class symbols in a row.  Definition 6.18 of [TakeutiZaring] p. 29
     generalized to arbitrary classes.  Class ` R ` normally denotes a relation
     such as " ` < ` " that compares two classes ` A ` and ` B ` , which might
     be numbers such as ` 1 ` and ` 2 ` .  This definition is well-defined,
     although not very meaningful, when classes ` A ` and/or ` B ` are proper
     classes (i.e. are not sets).  On the other hand, we often find uses for
     this definition when ` R ` is a proper class. $)
  df-br $a |- ( A R B <-> <. A , B >. e. R ) $.

  $( Equality theorem for binary relations. $)
  breq $p |- ( R = S -> ( A R B <-> A S B ) ) $=
    ( wceq cop wcel wbr eleq2 df-br 3bitr4g ) CDEABFZCGLDGABCHABDHCDLIABCJABDJK
    $.
    $( [4-Jun-1995] $)

  $( Equality theorem for a binary relation. $)
  breq1 $p |- ( A = B -> ( A R C <-> B R C ) ) $=
    ( wceq cop wcel wbr opeq1 eleq1d df-br 3bitr4g ) ABEZACFZDGBCFZDGACDHBCDHMN
    ODABCIJACDKBCDKL $.
    $( [31-Dec-1993] $)

  $( Equality theorem for a binary relation. $)
  breq2 $p |- ( A = B -> ( C R A <-> C R B ) ) $=
    ( wceq cop wcel wbr opeq2 eleq1d df-br 3bitr4g ) ABEZCAFZDGCBFZDGCADHCBDHMN
    ODABCIJCADKCBDKL $.
    $( [31-Dec-1993] $)

  $( Equality theorem for a binary relation. $)
  breq12 $p |- ( ( A = B /\ C = D ) -> ( A R C <-> B R D ) ) $=
    ( wceq wbr breq1 breq2 sylan9bb ) ABFACEGBCEGCDFBDEGABCEHCDBEIJ $.
    $( [8-Feb-1996] $)

  ${
    breqi.1 $e |- R = S $.
    $( Equality inference for binary relations. $)
    breqi $p |- ( A R B <-> A S B ) $=
      ( wceq wbr wb breq ax-mp ) CDFABCGABDGHEABCDIJ $.
      $( [20-Feb-2005] $) $( [19-Feb-2005] $)
  $}

  ${
    breq1i.1 $e |- A = B $.
    $( Equality inference for a binary relation. $)
    breq1i $p |- ( A R C <-> B R C ) $=
      ( wceq wbr wb breq1 ax-mp ) ABFACDGBCDGHEABCDIJ $.
      $( [8-Feb-1996] $)

    $( Equality inference for a binary relation. $)
    breq2i $p |- ( C R A <-> C R B ) $=
      ( wceq wbr wb breq2 ax-mp ) ABFCADGCBDGHEABCDIJ $.
      $( [8-Feb-1996] $)

    ${
      breq12i.2 $e |- C = D $.
      $( Equality inference for a binary relation.  (The proof was shortened by
         Eric Schmidt, 4-Apr-2007.) $)
      breq12i $p |- ( A R C <-> B R D ) $=
        ( wceq wbr wb breq12 mp2an ) ABHCDHACEIBDEIJFGABCDEKL $.
        $( [4-Apr-2007] $) $( [8-Feb-1996] $)
    $}
  $}

  ${
    breq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for a binary relation. $)
    breq1d $p |- ( ph -> ( A R C <-> B R C ) ) $=
      ( wceq wbr wb breq1 syl ) ABCGBDEHCDEHIFBCDEJK $.
      $( [8-Feb-1996] $)

    $( Equality deduction for a binary relation. $)
    breq2d $p |- ( ph -> ( C R A <-> C R B ) ) $=
      ( wceq wbr wb breq2 syl ) ABCGDBEHDCEHIFBCDEJK $.
      $( [8-Feb-1996] $)

    ${
      breq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for a binary relation.  (The proof was shortened by
         Andrew Salmon, 9-Jul-2011.) $)
      breq12d $p |- ( ph -> ( A R C <-> B R D ) ) $=
        ( wceq wbr wb breq12 sylanc ) BCIDEIBDFJCEFJKABCDEFLGHM $.
        $( [9-Jul-2011] $) $( [8-Feb-1996] $)

      $( Equality deduction for a binary relation. $)
      breq12dOLD $p |- ( ph -> ( A R C <-> B R D ) ) $=
        ( wbr breq1d breq2d bitrd ) ABDFICDFICEFIABCDFGJADECFHKL $.
        $( [8-Feb-1996] $)
    $}

    ${
      breqan12i.2 $e |- ( ps -> C = D ) $.
      $( Equality deduction for a binary relation. $)
      breqan12d $p |- ( ( ph /\ ps ) -> ( A R C <-> B R D ) ) $=
        ( wceq wbr wb breq12 syl2an ) CDJEFJCEGKDFGKLABCDEFGMHIN $.
        $( [8-Feb-1996] $)

      $( Equality deduction for a binary relation. $)
      breqan12rd $p |- ( ( ps /\ ph ) -> ( A R C <-> B R D ) ) $=
        ( wbr wb breqan12d ancoms ) ABCEGJDFGJKABCDEFGHILM $.
        $( [8-Feb-1996] $)
    $}
  $}

  ${
    eqbrtr.1 $e |- A = B $.
    eqbrtr.2 $e |- B R C $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtri $p |- A R C $=
      ( wbr breq1i mpbir ) ACDGBCDGFABCDEHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    eqbrtrd.1 $e |- ( ph -> A = B ) $.
    eqbrtrd.2 $e |- ( ph -> B R C ) $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrd $p |- ( ph -> A R C ) $=
      ( wbr breq1d mpbird ) ABDEHCDEHGABCDEFIJ $.
      $( [8-Oct-1999] $)
  $}

  ${
    eqbrtrr.1 $e |- A = B $.
    eqbrtrr.2 $e |- A R C $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrri $p |- B R C $=
      ( eqcomi eqbrtri ) BACDABEGFH $.
      $( [5-Aug-1993] $)
  $}

  ${
    eqbrtrrd.1 $e |- ( ph -> A = B ) $.
    eqbrtrrd.2 $e |- ( ph -> A R C ) $.
    $( Substitution of equal classes into a binary relation. $)
    eqbrtrrd $p |- ( ph -> B R C ) $=
      ( eqcomd eqbrtrd ) ACBDEABCFHGI $.
      $( [24-Oct-1999] $)
  $}

  ${
    breqtr.1 $e |- A R B $.
    breqtr.2 $e |- B = C $.
    $( Substitution of equal classes into a binary relation. $)
    breqtri $p |- A R C $=
      ( wbr breq2i mpbi ) ABDGACDGEBCADFHI $.
      $( [5-Aug-1993] $)
  $}

  ${
    breqtrd.1 $e |- ( ph -> A R B ) $.
    breqtrd.2 $e |- ( ph -> B = C ) $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrd $p |- ( ph -> A R C ) $=
      ( wbr breq2d mpbid ) ABCEHBDEHFACDBEGIJ $.
      $( [24-Oct-1999] $)
  $}

  ${
    breqtrr.1 $e |- A R B $.
    breqtrr.2 $e |- C = B $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrri $p |- A R C $=
      ( eqcomi breqtri ) ABCDECBFGH $.
      $( [5-Aug-1993] $)
  $}

  ${
    breqtrrd.1 $e |- ( ph -> A R B ) $.
    breqtrrd.2 $e |- ( ph -> C = B ) $.
    $( Substitution of equal classes into a binary relation. $)
    breqtrrd $p |- ( ph -> A R C ) $=
      ( eqcomd breqtrd ) ABCDEFADCGHI $.
      $( [24-Oct-1999] $)
  $}

  ${
    3brtr3.1 $e |- A R B $.
    3brtr3.2 $e |- A = C $.
    3brtr3.3 $e |- B = D $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3i $p |- C R D $=
      ( eqbrtrri breqtri ) CBDEACBEGFIHJ $.
      $( [11-Aug-1999] $)
  $}

  ${
    3brtr4.1 $e |- A R B $.
    3brtr4.2 $e |- C = A $.
    3brtr4.3 $e |- D = B $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr4i $p |- C R D $=
      ( eqbrtri breqtrri ) CBDECABEGFIHJ $.
      $( [11-Aug-1999] $)
  $}

  ${
    3brtr3d.1 $e |- ( ph -> A R B ) $.
    3brtr3d.2 $e |- ( ph -> A = C ) $.
    3brtr3d.3 $e |- ( ph -> B = D ) $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3d $p |- ( ph -> C R D ) $=
      ( wbr breq12d mpbid ) ABCFJDEFJGABDCEFHIKL $.
      $( [18-Oct-1999] $)
  $}

  ${
    3brtr4d.1 $e |- ( ph -> A R B ) $.
    3brtr4d.2 $e |- ( ph -> C = A ) $.
    3brtr4d.3 $e |- ( ph -> D = B ) $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr4d $p |- ( ph -> C R D ) $=
      ( wbr breq12d mpbird ) ADEFJBCFJGADBECFHIKL $.
      $( [8-Mar-2005] $) $( [21-Feb-2005] $)
  $}

  ${
    3brtr3g.1 $e |- ( ph -> A R B ) $.
    3brtr3g.2 $e |- A = C $.
    3brtr3g.3 $e |- B = D $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr3g $p |- ( ph -> C R D ) $=
      ( wbr breq12i sylib ) ABCFJDEFJGBDCEFHIKL $.
      $( [16-Jan-1997] $)
  $}

  ${
    3brtr4g.1 $e |- ( ph -> A R B ) $.
    3brtr4g.2 $e |- C = A $.
    3brtr4g.3 $e |- D = B $.
    $( Substitution of equality into both sides of a binary relation. $)
    3brtr4g $p |- ( ph -> C R D ) $=
      ( wbr breq12i sylibr ) ABCFJDEFJGDBECFHIKL $.
      $( [16-Jan-1997] $)
  $}

  ${
    syl5eqbr.1 $e |- ( ph -> A R B ) $.
    syl5eqbr.2 $e |- C = A $.
    $( A chained equality inference for a binary relation. $)
    syl5eqbr $p |- ( ph -> C R B ) $=
      ( eqid 3brtr4g ) ABCDCEFGCHI $.
      $( [11-Oct-1999] $)
  $}

  ${
    syl5eqbrr.1 $e |- ( ph -> A R B ) $.
    syl5eqbrr.2 $e |- A = C $.
    $( A chained equality inference for a binary relation. $)
    syl5eqbrr $p |- ( ph -> C R B ) $=
      ( eqid 3brtr3g ) ABCDCEFGCHI $.
      $( [23-Oct-2004] $) $( [17-Sep-2004] $)
  $}

  ${
    syl5breq.1 $e |- ( ph -> A = B ) $.
    syl5breq.2 $e |- C R A $.
    $( A chained equality inference for a binary relation. $)
    syl5breq $p |- ( ph -> C R B ) $=
      ( wbr a1i breqtrd ) ADBCEDBEHAGIFJ $.
      $( [11-Oct-1999] $)
  $}

  ${
    syl5breqr.1 $e |- ( ph -> B = A ) $.
    syl5breqr.2 $e |- C R A $.
    $( A chained equality inference for a binary relation. $)
    syl5breqr $p |- ( ph -> C R B ) $=
      ( eqcomd syl5breq ) ABCDEACBFHGI $.
      $( [26-Apr-2005] $) $( [24-Apr-2005] $)
  $}

  ${
    syl6eqbr.1 $e |- ( ph -> A = B ) $.
    syl6eqbr.2 $e |- B R C $.
    $( A chained equality inference for a binary relation. $)
    syl6eqbr $p |- ( ph -> A R C ) $=
      ( wbr breq1d mpbiri ) ABDEHCDEHGABCDEFIJ $.
      $( [12-Oct-1999] $)
  $}

  ${
    syl6eqbrr.1 $e |- ( ph -> B = A ) $.
    syl6eqbrr.2 $e |- B R C $.
    $( A chained equality inference for a binary relation. $)
    syl6eqbrr $p |- ( ph -> A R C ) $=
      ( eqcomd syl6eqbr ) ABCDEACBFHGI $.
      $( [7-Mar-2006] $) $( [4-Jan-2006] $)
  $}

  ${
    syl6breq.1 $e |- ( ph -> A R B ) $.
    syl6breq.2 $e |- B = C $.
    $( A chained equality inference for a binary relation. $)
    syl6breq $p |- ( ph -> A R C ) $=
      ( eqid 3brtr3g ) ABCBDEFBHGI $.
      $( [11-Oct-1999] $)
  $}

  ${
    syl6breqr.1 $e |- ( ph -> A R B ) $.
    syl6breqr.2 $e |- C = B $.
    $( A chained equality inference for a binary relation. $)
    syl6breqr $p |- ( ph -> A R C ) $=
      ( eqcomi syl6breq ) ABCDEFDCGHI $.
      $( [26-Apr-2005] $) $( [24-Apr-2005] $)
  $}


  ${
    ssbrd.1 $e |- ( ph -> A C_ B ) $.
    $( Deduction from a subclass relationship of binary relations. $)
    ssbrd $p |- ( ph -> ( C A D -> C B D ) ) $=
      ( cop wcel wbr sseld df-br 3imtr4g ) ADEGZBHMCHDEBIDECIABCMFJDEBKDECKL $.
      $( [14-May-2004] $) $( [30-Apr-2004] $)
  $}

  ${
    ssbri.1 $e |- A C_ B $.
    $( Inference from a subclass relationship of binary relations.  (The proof
       was shortened by Andrew Salmon, 9-Jul-2011.) $)
    ssbri $p |- ( C A D -> C B D ) $=
      ( wss wbr wi ssid a1i ssbrd ax-mp ) AAFZCDAGCDBGHAIMABCDABFMEJKL $.
      $( [9-Jul-2011] $) $( [28-Mar-2007] $)

    $( Inference from a subclass relationship of binary relations. $)
    ssbriOLD $p |- ( C A D -> C B D ) $=
      ( wceq wbr wi eqid wss a1i ssbrd ax-mp ) AAFZCDAGCDBGHAINABCDABJNEKLM $.
      $( [28-Mar-2007] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y R $.  $d x y $.
    hbbr.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbbr.2 $e |- ( y e. R -> A. x y e. R ) $.
    hbbr.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for binary relation. $)
    hbbr $p |- ( A R B -> A. x A R B ) $=
      ( cop wcel wal wbr hbop hbel df-br albii 3imtr4i ) CDIZEJZSAKCDELZTAKABBR
      EABCDFHMGNCDEOZTSAUAPQ $.
      $( [1-Sep-1999] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z R $.  $d x y z $.  $d y ph $.
    hbbrd.1 $e |- ( ph -> A. x ph ) $.
    hbbrd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbbrd.3 $e |- ( ph -> ( y e. R -> A. x y e. R ) ) $.
    hbbrd.4 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbbr .  (The
       proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    hbbrd $p |- ( ph -> ( A R B -> A. x A R B ) ) $=
      ( cop wcel wal wbr hbopd hbeld df-br albii 3imtr4g ) ADEKZFLZUABMDEFNZUBB
      MABCTFGABCDEGHJOIPDEFQZUBUABUCRS $.
      $( [9-Jul-2011] $) $( [13-Dec-2005] $)

    $( Deduction version of bound-variable hypothesis builder ~ hbbr . $)
    hbbrdOLD $p |- ( ph -> ( A R B -> A. x A R B ) ) $=
      ( vz cv wcel wal cab wbr wi hba1 hbab hbbr a1i wceq 19.21aiv abidhb syl
      breq12d wb breq bitrd albid 3imtr3d ) AKLZDMZBNZKOZULEMZBNZKOZULFMZBNZKOZ
      PZVBBNZDEFPZVDBNVBVCQABCUOURVAUNBKCUMBRSUTBKCUSBRSUQBKCUPBRSTUAAVBDEVAPZV
      DAUODUREVAACLZDMZVGBNQZCNUODUBAVHCHUCBCKDUDUEAVFEMZVIBNQZCNUREUBAVJCJUCBC
      KEUDUEUFAVAFUBZVEVDUGAVFFMZVLBNQZCNVKAVMCIUCBCKFUDUEDEVAFUHUEUIZAVBVDBGVN
      UJUK $.
      $( [22-Dec-2005] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y z $.  $d w x $.  $d w R z $.  $d w A z $.
    $( Relationship between a binary relation and a class abstraction.
       (Contributed by Andrew Salmon, 8-Jul-2011.) $)
    brab1 $p |- ( x R A <-> x e. { z | z R A } ) $=
      ( vw cv wbr wsbc cab wcel ax-17 sbco2 breq1 sbie sbbii bitr2i df-clab
      3bitr4i ) BFZCDGZBEFZHZEAFZHZTBUCHUCCDGZUCTBIJTBAETEKLUDUACDGZEUCHUEUBUFE
      ATUFBEUFBKSUACDMNOUFUEEAUEEKUAUCCDMNPTABQR $.
      $( [9-Jul-2011] $) $( [7-Apr-1994] $)

    $d x z $.
    $( Relationship between a binary relation and a class abstraction. $)
    brab1OLD $p |- ( x R y <-> x e. { z | z R y } ) $=
      ( cv wbr cab wcel visset breq1 elab bicomi ) AEZCEZBEZDFZCGHMODFZPQCMAINM
      ODJKL $.
      $( [7-Apr-1994] $)
  $}

  $( A property of proper class as the second argument of a binary relation. $)
  brprc $p |- ( -. B e. _V -> ( A R B <-> A R A ) ) $=
    ( cvv wcel wn cop wbr opprc2 eleq1d df-br 3bitr4g ) BDEFZABGZCEAAGZCEABCHAA
    CHMNOCABIJABCKAACKL $.
    $( [14-Jun-1998] $)

  $( The union of two binary relations. $)
  brun $p |- ( A ( R u. S ) B <-> ( A R B \/ A S B ) ) $=
    ( cop cun wcel wo wbr elun df-br orbi12i 3bitr4i ) ABEZCDFZGNCGZNDGZHABOIAB
    CIZABDIZHNCDJABOKRPSQABCKABDKLM $.
    $( [31-Dec-2008] $) $( [21-Dec-2008] $)

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d y z D $.  $d y z R $.
    $d x y z $.
    $( Move substitution in and out of a binary relation.  (The proof was
       shortened by Andrew Salmon, 9-Jul-2011.) $)
    sbcbrg $p |- ( A e. D -> ( [ A / x ] B R C <->
           [_ A / x ]_ B [_ A / x ]_ R [_ A / x ]_ C ) ) $=
      ( vy vz wbr cv wsbc csb dfsbcq wceq wb csbeq1 breq syl breq12d bitrd
      visset wcel ax-17 hbcsb1 hbbr csbeq1a sbie vtoclbg ) CDFIZAGJZKAUJCLZAUJD
      LZAUJFLZIZUIABKABCLZABDLZABFLZIZGBEUIAUJBMUJBNZUNUKULUQIZURUSUMUQNUNUTOAU
      JBFPUKULUMUQQRUSUKUOULUPUQAUJBCPAUJBDPSTUIUNAGAHUKULUMAHUJCGUAZHJUJUBAUCZ
      UDAHUJFVAVBUDAHUJDVAVBUDUEAJUJNZUICDUMIZUNVCFUMNUIVDOAUJFUFCDFUMQRVCCUKDU
      LUMAUJCUFAUJDUFSTUGUH $.
      $( [9-Jul-2011] $) $( [13-Dec-2005] $)

    $( Move substitution in and out of a binary relation. $)
    sbcbrgOLD $p |- ( A e. D -> ( [ A / x ] B R C <->
           [_ A / x ]_ B [_ A / x ]_ R [_ A / x ]_ C ) ) $=
      ( vy vz wcel wbr cv wsbc csb ax-17 hbcsb1g hbbrd wceq wb wex a9e cvv wal
      wi visset hbsbc1g ax-mp hbcsb1 hbbr hbbi csbeq1a breq12d sbceq1a breq
      syl 3bitr3d 19.23ai a1i 3bitrd sbciegf sbccog csbcog bitrd ) BEIZCDFJZAGK
      ZLZGBLGBAVECMZMZGBAVEDMZMZGBAVEFMZMZJZVDABLABCMZABDMZABFMZJZVFVMGBEVCGHVH
      VJVLVCGNGHBVGEHKZBIGNZOGHBVKEVSOGHBVIEVSOPVEBQZVFVGVIVKJZVHVJVKJZVMVFWARZ
      VTAKVEQZASWCAGTWDWCAVFWAAVEUAIVFVFAUBUCGUDZVDAHVEUAVRVEIANZUEUFAHVGVIVKAH
      VECWEWFUGAHVEFWEWFUGAHVEDWEWFUGUHUIWDVDVGVIFJZVFWAWDCVGDVIFAVECUJAVEDUJUK
      VDAVEULWDFVKQWGWARAVEFUJVGVIFVKUMUNUOUPUFUQVTVGVHVIVJVKGBVGUJGBVIUJUKVTVK
      VLQWBVMRGBVKUJVHVJVKVLUMUNURUSVDAGBEUTVCVMVNVOVLJZVQVCVHVNVJVOVLAGBCEVAAG
      BDEVAUKVCVLVPQWHVQRAGBFEVAVNVOVLVPUMUNVBUO $.
      $( [22-Dec-2005] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d y C $.  $d y D $.  $d x y R $.
    $( Move substitution in and out of a binary relation. $)
    sbcbr12g $p |- ( A e. D ->
                 ( [ A / x ] B R C <-> [_ A / x ]_ B R [_ A / x ]_ C ) ) $=
      ( vy wcel wbr wsbc csb sbcbrg wceq wb cv ax-17 csbconstgf breq syl bitrd
      ) BEHZCDFIABJABCKZABDKZABFKZIZUBUCFIZABCDEFLUAUDFMUEUFNAGBFEGOFHAPQUBUCUD
      FRST $.
      $( [30-Dec-2005] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x y C $.  $d y D $.  $d x y R $.
    $( Move substitution in and out of a binary relation. $)
    sbcbr1g $p |- ( A e. D ->
                  ( [ A / x ] B R C <-> [_ A / x ]_ B R C ) ) $=
      ( vy wcel wbr wsbc csb sbcbr12g cv ax-17 csbconstgf breq2d bitrd ) BEHZCD
      FIABJABCKZABDKZFISDFIABCDEFLRTDSFAGBDEGMDHANOPQ $.
      $( [1-Jan-2006] $) $( [13-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y D $.  $d x y R $.
    $( Move substitution in and out of a binary relation. $)
    sbcbr2g $p |- ( A e. D ->
                  ( [ A / x ] B R C <-> B R [_ A / x ]_ C ) ) $=
      ( vy wcel wbr wsbc csb sbcbr12g cv ax-17 csbconstgf breq1d bitrd ) BEHZCD
      FIABJABCKZABDKZFICTFIABCDEFLRSCTFAGBCEGMCHANOPQ $.
      $( [21-Feb-2006] $) $( [13-Dec-2005] $)
  $}

