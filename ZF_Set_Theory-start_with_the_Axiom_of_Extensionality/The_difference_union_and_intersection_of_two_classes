$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for class difference. $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 anbi1d abbidv df-dif 3eqtr4g ) ABE
      ZDFZAGZRCGHZIZDJRBGZTIZDJACKBCKQUAUCDQSUBTABRLMNDACODBCOP $.
      $( [10-Feb-1997] $)

    $( Equality theorem for class difference. $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn wa cab cdif eleq2 negbid anbi2d abbidv df-dif
      3eqtr4g ) ABEZDFZCGZSAGZHZIZDJTSBGZHZIZDJCAKCBKRUCUFDRUBUETRUAUDABSLMNODC
      APDCBPQ $.
      $( [10-Feb-1997] $)
  $}

  ${
    difeq1i.1 $e |- A = B $.
    $( Inference adding difference to the right in a class equality. $)
    difeq1i $p |- ( A \ C ) = ( B \ C ) $=
      ( wceq cdif difeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [15-Nov-2002] $)

    $( Inference adding difference to the left in a class equality. $)
    difeq2i $p |- ( C \ A ) = ( C \ B ) $=
      ( wceq cdif difeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [15-Nov-2002] $)

    ${
      difeq12i.2 $e |- C = D $.
      $( Equality inference for class difference. $)
      difeq12i $p |- ( A \ C ) = ( B \ D ) $=
        ( cdif difeq1i difeq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
        $( [11-Sep-2004] $) $( [29-Aug-2004] $)
    $}
  $}

  ${
    difeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding difference to the right in a class equality. $)
    difeq1d $p |- ( ph -> ( A \ C ) = ( B \ C ) ) $=
      ( wceq cdif difeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [15-Nov-2002] $)

    $( Deduction adding difference to the left in a class equality. $)
    difeq2d $p |- ( ph -> ( C \ A ) = ( C \ B ) ) $=
      ( wceq cdif difeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [15-Nov-2002] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    difeqri.1 $e |- ( ( x e. A /\ -. x e. B ) <-> x e. C ) $.
    $( Inference from membership to difference. $)
    difeqri $p |- ( A \ B ) = C $=
      ( cdif cv wcel wn wa cab df-dif bicomi abbi2i eqtr4 ) BCFAGZBHPCHIJZAKDAB
      CLQADQPDHEMNO $.
      $( [17-May-1998] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbdif.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbdif.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for class difference. $)
    hbdif $p |- ( y e. ( A \ B ) -> A. x y e. ( A \ B ) ) $=
      ( cv wcel wn wa wal cdif hbn hban eldif albii 3imtr4 ) BGZCHZRDHZIZJZUBAK
      RCDLHZUCAKSUAAETAFMNRCDOZUCUBAUDPQ $.
      $( [3-Dec-2003] $) $( [3-Dec-2003] $)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
    ( cdif wcel wn eldif pm3.26bi ) ABCDEABEACEFABCGH $.
    $( [29-Apr-1994] $)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
    ( cdif wcel wn eldif pm3.27bi ) ABCDEABEACEFABCGH $.
    $( [3-May-1994] $)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
    ( cdif wcel eldifn con2i ) ACBDEABEACBFG $.
    $( [27-Jun-1994] $)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
    ( wcel cdif wn wa eldif biimpr ex con1d imp ) ABDZABCEDZFACDZMONMOFZNNMPGAB
    CHIJKL $.
    $( [28-Jun-1994] $)

  ${
    $d x A $.  $d x B $.
    $( Double class difference.  Exercise 11 of [TakeutiZaring] p. 22. $)
    difdif $p |- ( A \ ( B \ A ) ) = A $=
      ( vx cdif cv wcel wn wa wi eldif negbii iman bitr4 anbi2i pm4.45im
      difeqri ) CABADZACEZAFZRQFZGZHSRBFZSIZHSUAUCSUAUBSGHZGUCTUDRBAJKUBSLMNSUB
      OMP $.
      $( [17-May-1998] $)

    $( Subclass relationship for class difference.  Exercise 14 of
       [TakeutiZaring] p. 22. $)
    difss $p |- ( A \ B ) (_ A $=
      ( vx cdif cv eldifi ssriv ) CABDACEABFG $.
      $( [29-Apr-1994] $)
  $}

  $( Preservation of a subclass relationship by class difference. $)
  ssdifss $p |- ( A (_ B -> ( A \ C ) (_ B ) $=
    ( cdif wss difss sstr mpan ) ACDZAEABEIBEACFIABGH $.
    $( [26-Feb-2007] $) $( [15-Feb-2007] $)

  ${
    $d x A $.
    $( Double complement under universal class.  Exercise 4.10(s) of
       [Mendelson] p. 231. $)
    ddif $p |- ( V \ ( V \ A ) ) = A $=
      ( vx cvv cdif cv wcel wn wa eldif visset mpbiran con2bii biantrur bitr2
      difeqri ) BCCADZABEZAFZQPFZGZQCFZTHSRSUARGQCAIBJZKLUATUBMNO $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Contraposition law for subsets. $)
    ssconb $p |- ( ( A (_ C /\ B (_ C ) ->
                  ( A (_ ( C \ B ) <-> B (_ ( C \ A ) ) ) $=
      ( vx wss wa cv wcel cdif wi wal wn wb pm5.1 ssel syl2an bi2.03 a1i
      anbi12d jcab 3bitr4g eldif imbi2i albidv dfss2 ) ACEZBCEZFZDGZAHZUICBIZHZ
      JZDKUIBHZUICAIZHZJZDKAUKEBUOEUHUMUQDUHUJUICHZUNLZFZJZUNURUJLZFZJZUMUQUHUJ
      URJZUJUSJZFUNURJZUNVBJZFVAVDUHVEVGVFVHVEVGVEVGMUFUGVEVGNACUIOBCUIOPVFVHMU
      HUJUNQRSUJURUSTUNURVBTUAULUTUJUICBUBUCUPVCUNUICAUBUCUAUDDAUKUEDBUOUEUA $.
      $( [22-Mar-1998] $)

    $( Contraposition law for subsets.  Exercise 15 of [TakeutiZaring]
       p. 22. $)
    sscon $p |- ( A (_ B -> ( C \ B ) (_ ( C \ A ) ) $=
      ( vx wss cdif cv wcel wn wa ssel con3d anim2d eldif 3imtr4g ssrdv ) ABEZD
      CBFZCAFZQDGZCHZTBHZIZJUATAHZIZJTRHTSHQUCUEUAQUDUBABTKLMTCBNTCANOP $.
      $( [22-Mar-1998] $)

    $( Difference law for subsets. $)
    ssdif $p |- ( A (_ B -> ( A \ C ) (_ ( B \ C ) ) $=
      ( vx wss cdif cv wcel wn wa ssel anim1d eldif 3imtr4g ssrdv ) ABEZDACFZBC
      FZPDGZAHZSCHIZJSBHZUAJSQHSRHPTUBUAABSKLSACMSBCMNO $.
      $( [28-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in class union.  Theorem 12 of [Suppes]
       p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cun wcel cvv wo elisset jaoi cv wceq eleq1 orbi12d df-un elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARITSUAABIACIJDKZBFZUCCFZHUBDARGUCALUDTUE
      UAUCABMUCACMNDBCOPQ $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( ( x e. A \/ x e. B ) <-> x e. C ) $.
    $( Inference from membership to union. $)
    uneqri $p |- ( A u. B ) = C $=
      ( cun cv wcel wo elun bitr eqriv ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for union of classes.  Theorem 23 of [Suppes] p. 27. $)
    unidm $p |- ( A u. A ) = A $=
      ( vx cv wcel oridm uneqri ) BAAABCADEF $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Commutative law for union of classes.  Exercise 6 of [TakeutiZaring]
       p. 17. $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cun cv wcel wo orcom elun 3bitr4 eqriv ) CABDZBADZCEZAFZNBFZGPOGNLFN
      MFOPHNABINBAIJK $.
      $( [25-Jun-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cun cv wcel wo eleq2 orbi1d elun 3bitr4g eqrdv ) ABEZDACFZBCFZO
      DGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [5-Aug-1993] $)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
    ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [5-Aug-1993] $)

  $( Equality theorem for union of two classes. $)
  uneq12 $p |- ( ( A = B /\ C = D ) -> ( A u. C ) = ( B u. D ) ) $=
    ( wceq cun uneq1 uneq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [29-Mar-1998] $)

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i $p |- ( A u. C ) = ( B u. C ) $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [30-Aug-1993] $)

    $( Inference adding union to the left in a class equality. $)
    uneq2i $p |- ( C u. A ) = ( C u. B ) $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [30-Aug-1993] $)

    ${
      uneq12i.2 $e |- C = D $.
      $( Equality inference for union of two classes.  (The proof was
         shortened by Eric Schmidt, 26-Jan-2007.) $)
      uneq12i $p |- ( A u. C ) = ( B u. D ) $=
        ( wceq cun uneq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
        $( [27-Jan-2007] $) $( [12-Aug-2004] $)
    $}
  $}

  ${
    uneq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the right in a class equality. $)
    uneq1d $p |- ( ph -> ( A u. C ) = ( B u. C ) ) $=
      ( wceq cun uneq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [29-Mar-1998] $)

    $( Deduction adding union to the left in a class equality. $)
    uneq2d $p |- ( ph -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [29-Mar-1998] $)

    ${
      uneq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for intersection of two classes. $)
      uneq12d $p |- ( ph -> ( A u. C ) = ( B u. D ) ) $=
        ( cun uneq1d uneq2d eqtrd ) ABDHCDHCEHABCDFIADECGJK $.
        $( [30-Sep-2004] $) $( [29-Sep-2004] $)
    $}
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbun.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbun.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the union of classes. $)
    hbun $p |- ( y e. ( A u. B ) -> A. x y e. ( A u. B ) ) $=
      ( cv wcel wo wal cun hbor elun albii 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRA
      EFLPCDMZTSAUANO $.
      $( [15-Sep-2003] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Associative law for union of classes.  Exercise 8 of [TakeutiZaring]
       p. 17. $)
    unass $p |- ( ( A u. B ) u. C ) = ( A u. ( B u. C ) ) $=
      ( vx cun cv wcel wo orass elun orbi1i orbi2i 3bitr4 bitr4 uneqri ) DABEZC
      ABCEZEZDFZPGZSCGZHZSAGZSQGZHZSRGUCSBGZHZUAHUCUFUAHZHUBUEUCUFUAITUGUASABJK
      UDUHUCSBCJLMSAQJNO $.
      $( [3-May-1994] $)
  $}

  $( A rearrangement of union. $)
  un12 $p |- ( A u. ( B u. C ) ) = ( B u. ( A u. C ) ) $=
    ( cun uncom uneq1i unass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $( [26-Aug-2004] $) $( [12-Aug-2004] $)

  $( A rearrangement of union. $)
  un23 $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. B ) $=
    ( cun uncom uneq2i unass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $( [29-Aug-2004] $) $( [12-Aug-2004] $)

  $( A rearrangement of the union of 4 classes. $)
  un4 $p |- ( ( A u. B ) u. ( C u. D ) ) =
            ( ( A u. C ) u. ( B u. D ) ) $=
    ( cun un12 uneq2i unass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $( [30-Aug-2004] $) $( [12-Aug-2004] $)

  $( Union distributes over itself. $)
  unundi $p |- ( A u. ( B u. C ) ) = ( ( A u. B ) u. ( A u. C ) ) $=
    ( cun unidm uneq1i un4 eqtr3 ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [15-Sep-2004] $) $( [17-Aug-2004] $)

  $( Union distributes over itself. $)
  unundir $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. ( B u. C ) ) $=
    ( cun unidm uneq2i un4 eqtr3 ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [9-Sep-2004] $) $( [17-Aug-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Subclass relationship for union of classes.  Theorem 25 of [Suppes]
       p. 27. $)
    ssun1 $p |- A (_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssriv ) CAABDZCEZAFZNMBFZGMLFNOHMABIJ
      K $.
      $( [5-Aug-1993] $)
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A (_ ( B u. A ) $=
    ( cun ssun1 uncom sseqtr ) AABCBACABDABEF $.
    $( [30-Aug-1993] $)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A (_ B -> A (_ ( B u. C ) ) $=
    ( wss cun ssun1 sstr2 mpi ) ABDBBCEZDAIDBCFABIGH $.
    $( [5-Aug-1993] $)

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A (_ B -> A (_ ( C u. B ) ) $=
    ( wss cun ssun2 sstr2 mpi ) ABDBCBEZDAIDBCFABIGH $.
    $( [14-Aug-1994] $)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
    ( cun ssun1 sseli ) BBCDABCEF $.
    $( [5-Aug-1993] $)

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
    ( cun ssun2 sseli ) BCBDABCEF $.
    $( [30-Aug-1993] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass law for union of classes. $)
    unss1 $p |- ( A (_ B -> ( A u. C ) (_ ( B u. C ) ) $=
      ( vx cv wcel wi wal cun wss wo pm2.38 elun 3imtr4g 19.20i dfss2 3imtr4 )
      DEZAFZRBFZGZDHRACIZFZRBCIZFZGZDHABJUBUDJUAUFDUASRCFZKTUGKUCUEUGSTLRACMRBC
      MNODABPDUBUDPQ $.
      $( [14-Oct-1999] $)

    $( A relationship between subclass and union.  Theorem 26 of [Suppes]
       p. 27. $)
    ssequn1 $p |- ( A (_ B <-> ( A u. B ) = B ) $=
      ( vx cun wceq cv wcel wo cab wss df-un eqeq2i eqcom wi wal wb pm4.72
      albii dfss2 abeq2 3bitr4 3bitr4r ) BABDZEBCFZAGZUDBGZHZCIZEZUCBEABJZUCUHB
      CABKLUCBMUEUFNZCOUFUGPZCOUJUIUKULCUEUFQRCABSUGCBTUAUB $.
      $( [29-Aug-2004] $) $( [30-Aug-1993] $)
  $}

  $( Subclass law for union of classes.  Exercise 7 of [TakeutiZaring]
     p. 18. $)
  unss2 $p |- ( A (_ B -> ( C u. A ) (_ ( C u. B ) ) $=
    ( wss cun unss1 uncom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [14-Oct-1999] $)

  $( Subclass law for union of classes. $)
  unss12 $p |- ( ( A (_ B /\ C (_ D ) -> ( A u. C ) (_ ( B u. D ) ) $=
    ( wss cun unss1 unss2 sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [7-Jun-2004] $) $( [2-Jun-2004] $)

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A (_ B <-> ( B u. A ) = B ) $=
    ( wss cun wceq ssequn1 uncom eqeq1i bitr ) ABCABDZBEBADZBEABFJKBABGHI $.
    $( [23-Sep-2004] $) $( [13-Jun-1994] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( The union of two subclasses is a subclass.  Theorem 27 of [Suppes] p. 27
       and its converse. $)
    unss $p |- ( ( A (_ C /\ B (_ C ) <-> ( A u. B ) (_ C ) $=
      ( vx cv cun wcel wi wal wa wss wo elun imbi1i jaob bitr albii dfss2
      anbi12i 19.26 bitr4 3bitr4r ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZUD
      CKACKZBCKZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUMUN
      URUOUSDACRDBCRSUIUKDTUAUB $.
      $( [15-Jun-2004] $) $( [11-Jun-2004] $)
  $}

  ${
    unssi.1 $e |- A (_ C $.
    unssi.2 $e |- B (_ C $.
    $( An inference that the union of two subclasses is a subclass.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    unssi $p |- ( A u. B ) (_ C $=
      ( wss wa cun pm3.2i unss mpbi ) ACFZBCFZGABHCFLMDEIABCJK $.
      $( [14-May-2003] $) $( [10-Dec-2002] $)
  $}

  $( A condition that implies inclusion in the union of two classes. $)
  ssun $p |- ( ( A (_ B \/ A (_ C ) -> A (_ ( B u. C ) ) $=
    ( wss cun ssun3 ssun4 jaoi ) ABDABCEDACDABCFACBGH $.
    $( [23-Nov-2003] $) $( [23-Nov-2003] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of [Suppes] p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cin wcel cvv wa elisset adantl cv wceq eleq1 anbi12d df-in elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARIUASTACIJDKZBFZUCCFZHUBDARGUCALUDTUEUAU
      CABMUCACMNDBCOPQ $.
      $( [29-Apr-1994] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Commutative law for intersection of classes.  Exercise 7 of
       [TakeutiZaring] p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cin cv wcel wa ancom elin 3bitr4 eqriv ) CABDZBADZCEZAFZNBFZGPOGNLFN
      MFOPHNABINBAIJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( ( x e. A /\ x e. B ) <-> x e. C ) $.
    $( Inference from membership to intersection. $)
    ineqri $p |- ( A i^i B ) = C $=
      ( cin cv wcel wa elin bitr eqriv ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cin cv wcel wa eleq2 anbi1d elin 3bitr4g eqrdv ) ABEZDACFZBCFZO
      DGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [14-Dec-1993] $)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
    ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [26-Dec-1993] $)

  $( Equality theorem for intersection of two classes. $)
  ineq12 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
    ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [8-May-1994] $)

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [26-Dec-1993] $)

    $( Equality inference for intersection of two classes. $)
    ineq2i $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [26-Dec-1993] $)

    ${
      ineq12i.2 $e |- C = D $.
      $( Equality inference for intersection of two classes.  (The proof was
         shortened by Eric Schmidt, 26-Jan-2007.) $)
      ineq12i $p |- ( A i^i C ) = ( B i^i D ) $=
        ( wceq cin ineq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
        $( [27-Jan-2007] $) $( [24-Jun-2004] $)
    $}
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [10-Apr-1994] $)

    $( Equality deduction for intersection of two classes. $)
    ineq2d $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [10-Apr-1994] $)

    ${
      ineq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for intersection of two classes. $)
      ineq12d $p |- ( ph -> ( A i^i C ) = ( B i^i D ) ) $=
        ( cin ineq1d ineq2d eqtrd ) ABDHCDHCEHABCDFIADECGJK $.
        $( [25-Jun-2004] $) $( [24-Jun-2004] $)
    $}

    ${
      ineqan12d.2 $e |- ( ps -> C = D ) $.
      $( Equality deduction for intersection of two classes. $)
      ineqan12d $p |- ( ( ph /\ ps ) -> ( A i^i C ) = ( B i^i D ) ) $=
        ( wceq cin ineq12 syl2an ) CDIEFICEJDFJIABCDEFKGHL $.
        $( [7-Feb-2007] $) $( [7-Feb-2007] $)
    $}
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbin.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbin.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the intersection of classes. $)
    hbin $p |- ( y e. ( A i^i B ) -> A. x y e. ( A i^i B ) ) $=
      ( cv wcel wa wal cin hban elin albii 3imtr4 ) BGZCHZPDHZIZSAJPCDKHZTAJQRA
      EFLPCDMZTSAUANO $.
      $( [15-Sep-2003] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    rabbirdv.1 $e |- ( ph -> ( x e. B -> ( x e. A <-> ch ) ) ) $.
    $( Deduction from wff to restricted class abstraction. $)
    rabbirdv $p |- ( ph -> ( B i^i A ) = { x e. B | ch } ) $=
      ( cin cv wcel wa cab crab pm5.32d elin syl5bb abbi2dv df-rab syl6eqr ) AE
      DGZCHZEIZBJZCKBCELAUBCSAUATDIZJUBTSIAUAUCBFMTEDNOPBCEQR $.
      $( [16-Sep-2004] $) $( [27-Aug-2004] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for intersection of classes.  Theorem 15 of [Suppes]
       p. 26. $)
    inidm $p |- ( A i^i A ) = A $=
      ( vx cv wcel anidm ineqri ) BAAABCADEF $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Associative law for intersection of classes.  Exercise 9 of
       [TakeutiZaring] p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa anass elin anbi2i bitr4 anbi1i 3bitr4 ineqri ) DABEZC
      ABCEZEZDFZAGZSBGZHZSCGZHZTSQGZHZSPGZUCHSRGUDTUAUCHZHUFTUAUCIUEUHTSBCJKLUG
      UBUCSABJMSAQJNO $.
      $( [3-May-1994] $)
  $}

  $( A rearrangement of intersection. $)
  in12 $p |- ( A i^i ( B i^i C ) ) = ( B i^i ( A i^i C ) ) $=
    ( cin incom ineq1i inass 3eqtr3 ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH $.
    $( [21-Apr-2001] $)

  $( A rearrangement of intersection. $)
  in23 $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i B ) $=
    ( cin incom ineq2i inass 3eqtr4 ) ABCDZDACBDZDABDCDACDBDIJABCEFABCGACBGH $.
    $( [21-Apr-2001] $)

  $( Rearrangement of intersection of 4 classes. $)
  in4 $p |- ( ( A i^i B ) i^i ( C i^i D ) ) =
            ( ( A i^i C ) i^i ( B i^i D ) ) $=
    ( cin in12 ineq2i inass 3eqtr4 ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHAC
    LHI $.
    $( [21-Apr-2001] $)

  $( Intersection distributes over itself. $)
  inindi $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
    ( cin inidm ineq1i in4 eqtr3 ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [6-May-1994] $)

  $( Intersection distributes over itself. $)
  inindir $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i ( B i^i C ) ) $=
    ( cin inidm ineq2i in4 eqtr3 ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [18-Sep-2004] $) $( [17-Aug-2004] $)

  $( A relationship between subclass and intersection.  Similar to
     Exercise 9 of [TakeutiZaring] p. 18. $)
  sseqin2 $p |- ( A (_ B <-> ( B i^i A ) = A ) $=
    ( wss cin wceq df-ss incom eqeq1i bitr ) ABCABDZAEBADZAEABFJKAABGHI $.
    $( [23-Sep-2004] $) $( [17-May-1994] $)

  ${
    $d x A $.  $d x B $.
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of [TakeutiZaring] p. 18. $)
    inss1 $p |- ( A i^i B ) (_ A $=
      ( vx cin cv wcel elin pm3.26bi ssriv ) CABDZACEZJFKAFKBFKABGHI $.
      $( [27-Apr-1994] $)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of [TakeutiZaring] p. 18. $)
  inss2 $p |- ( A i^i B ) (_ B $=
    ( cin incom inss1 eqsstr3 ) ABCBACBBADBAEF $.
    $( [27-Apr-1994] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of [Monk1] p. 26. $)
    ssin $p |- ( ( A (_ B /\ A (_ C ) <-> A (_ ( B i^i C ) ) $=
      ( wss wa cin wceq ineq12 inindi syl5eq inidm syl6eq df-ss anbi12i 3imtr4
      inss1 sstr2 mpi inss2 jca impbi ) ABDZACDZEZABCFZDZABFZAGZACFZAGZEZAUEFZA
      GUDUFUKULAAFZAUKUGUIFUMULUGAUIAHABCIJAKLUBUHUCUJABMACMNAUEMOUFUBUCUFUEBDU
      BBCPAUEBQRUFUECDUCBCSAUECQRTUA $.
      $( [16-Jun-2004] $) $( [15-Jun-2004] $)
  $}

  ${
    ssini.1 $e |- A (_ B $.
    ssini.2 $e |- A (_ C $.
    $( An inference showing that the a subclass of two classes is a subclass of
       their intersection. $)
    ssini $p |- A (_ ( B i^i C ) $=
      ( wss wa cin pm3.2i ssin mpbi ) ABFZACFZGABCHFLMDEIABCJK $.
      $( [24-Nov-2003] $) $( [24-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation. $)
    ssrin $p |- ( A (_ B -> ( A i^i C ) (_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal cin wss wa pm3.45 elin 3imtr4g 19.20i dfss2 3imtr4 )
      DEZAFZRBFZGZDHRACIZFZRBCIZFZGZDHABJUBUDJUAUFDUASRCFZKTUGKUCUESTUGLRACMRBC
      MNODABPDUBUDPQ $.
      $( [16-Aug-1994] $)

    $( Add left intersection to subclass relation. $)
    sslin $p |- ( A (_ B -> ( C i^i A ) (_ ( C i^i B ) ) $=
      ( wss cin ssrin incom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $( [19-Oct-1999] $)
  $}

  $( Intersection of subclasses. $)
  ss2in $p |- ( ( A (_ B /\ C (_ D ) -> ( A i^i C ) (_ ( B i^i D ) ) $=
    ( wss cin ssrin sslin sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [5-May-2000] $)

  $( Intersection preserves subclass relationship. $)
  ssinss1 $p |- ( A (_ C -> ( A i^i B ) (_ C ) $=
    ( cin wss wi inss1 sstr2 ax-mp ) ABDZAEACEJCEFABGJACHI $.
    $( [14-Sep-1999] $)

  $( Absorption law for union. $)
  unabs $p |- ( A u. ( A i^i B ) ) = A $=
    ( cin wss cun wceq inss1 ssequn2 mpbi ) ABCZADAJEAFABGJAHI $.
    $( [19-Apr-2006] $) $( [16-Apr-2006] $)

  $( Absorption law for intersection. $)
  inabs $p |- ( A i^i ( A u. B ) ) = A $=
    ( cun wss cin wceq ssun1 df-ss mpbi ) AABCZDAJEAFABGAJHI $.
    $( [23-Apr-2006] $) $( [16-Apr-2006] $)

  $( Negation of subclass expressed in terms of intersection and proper
     subclass. $)
  nssinpss $p |- ( -. A (_ B <-> ( A i^i B ) (. A ) $=
    ( cin wss wn wa wpss inss1 biantrur ssid ssin bitr negbii dfpss3 3bitr4 )
    AABCZDZEZPADZRFABDZEPAGSRABHITQTAADZTFQUATAJIAABKLMPANO $.
    $( [1-Jul-2004] $) $( [30-Jun-2004] $)

  $( Negation of subclass expressed in terms of proper subclass and union. $)
  nsspssun $p |- ( -. A (_ B <-> B (. ( A u. B ) ) $=
    ( cun wss wn wa wpss ssun2 biantrur ssid biantru unss bitr negbii dfpss3
    3bitr4 ) ABCZBDZEZBQDZSFABDZEBQGTSBAHIUARUAUABBDZFRUBUABJKABBLMNBQOP $.
    $( [18-Sep-2004] $) $( [15-Sep-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Subclass defined in terms of class difference.  See comments under
       ~ dfun2 . $)
    dfss4 $p |- ( A (_ B <-> ( B \ ( B \ A ) ) = A ) $=
      ( vx wss cin wceq cdif sseqin2 cv wcel wa wn wi abai iman anbi2i bitr
      elin eldif negbii 3bitr4 eqriv eqeq1i ) ABDBAEZAFBBAGZGZAFABHUDUFACUDUFCI
      ZBJZUGAJZKZUHUHUILKZLZKZUGUDJUGUFJZUJUHUHUIMZKUMUHUINUOULUHUHUIOPQUGBARUN
      UHUGUEJZLZKUMUGBUESUQULUHUPUKUGBASTPQUAUBUCQ $.
      $( [23-Sep-2004] $) $( [22-Mar-1998] $)

    $( An alternate definition of the union of two classes in terms of class
       difference, requiring no dummy variables.  Along with ~ dfin2 and
       ~ dfss4 it shows we can express union, intersection, and subset directly
       in terms of the single "primitive" operation ` \ `  (class
       difference). $)
    dfun2 $p |- ( A u. B ) = ( V \ ( ( V \ A ) \ B ) ) $=
      ( vx cun cvv cdif cv wcel wo wn wa eldif visset mpbiran anbi1i ioran
      3bitr4 con2bii elun eqriv ) CABDZEEAFZBFZFZCGZAHZUEBHZIZUEUCHZJZUEUAHUEUD
      HZUIUHUEUBHZUGJZKUFJZUMKUIUHJULUNUMULUEEHZUNUEEALCMZNOUEUBBLUFUGPQRUEABSU
      KUOUJUEEUCLUPNQT $.
      $( [12-Jun-2004] $) $( [10-Jun-2004] $)

    $( An alternate definition of the intersection of two classes in terms of
       class difference, requiring no dummy variables.  See comments under
       ~ dfun2 .  Another version is given by ~ dfin4 . $)
    dfin2 $p |- ( A i^i B ) = ( A \ ( V \ B ) ) $=
      ( vx cin cvv cdif cv wcel wa wn eldif visset mpbiran con2bii anbi2i elin
      3bitr4 eqriv ) CABDZAEBFZFZCGZAHZUBBHZIUCUBTHZJZIUBSHUBUAHUDUFUCUEUDUEUBE
      HUDJUBEBKCLMNOUBABPUBATKQR $.
      $( [12-Jun-2004] $) $( [10-Jun-2004] $)

    $( Difference with intersection.  Theorem 33 of [Suppes] p. 29. $)
    difin $p |- ( A \ ( A i^i B ) ) = ( A \ B ) $=
      ( vx cin cdif cv wcel wn wa wi abai imnan anbi2i bitr eldif elin negbii
      3bitr4r eqriv ) CAABDZEZABEZCFZAGZUCBGZHZIZUDUDUEIZHZIZUCUBGUCUAGZUGUDUDU
      FJZIUJUDUFKULUIUDUDUELMNUCABOUKUDUCTGZHZIUJUCATOUNUIUDUMUHUCABPQMNRS $.
      $( [18-Aug-2004] $) $( [31-Mar-1998] $)
  $}

  $( Union defined in terms of intersection (DeMorgan's law).  Definition of
     union in [Mendelson] p. 231. $)
  dfun3 $p |- ( A u. B ) = ( V \ ( ( V \ A ) i^i ( V \ B ) ) ) $=
    ( cun cvv cdif cin dfun2 dfin2 ddif difeq2i eqtr2 eqtr ) ABCDDAEZBEZEDMDBEZ
    FZEABGNPDPMDOEZENMOHQBMBIJKJL $.
    $( [8-Jan-2002] $)

  $( Intersection defined in terms of union (DeMorgan's law.  Similar to
     Exercise 4.10(n) of [Mendelson] p. 231. $)
  dfin3 $p |- ( A i^i B ) = ( V \ ( ( V \ A ) u. ( V \ B ) ) ) $=
    ( cvv cdif cun cin ddif dfun2 difeq1i difeq2i eqtr dfin2 3eqtr4r ) CCACBDZD
    ZDZDOCCADZNEZDABFOGRPCRCCQDZNDZDPQNHTOCSANAGIJKJABLM $.
    $( [8-Jan-2002] $)

  $( Alternate definition of the union of two classes.  Exercise 4.10(q) of
     [Mendelson] p. 231. $)
  dfin4 $p |- ( A i^i B ) = ( A \ ( A \ B ) ) $=
    ( cin cdif wss wceq inss1 dfss4 mpbi difin difeq2i eqtr3 ) AAABCZDZDZMAABDZ
    DMAEOMFABGMAHINPAABJKL $.
    $( [25-Nov-2003] $) $( [25-Nov-2003] $)

  $( Intersection with universal complement.  Remark in [Stoll] p. 20. $)
  invdif $p |- ( A i^i ( V \ B ) ) = ( A \ B ) $=
    ( cvv cdif cin dfin2 ddif difeq2i eqtr ) ACBDZEACJDZDABDAJFKBABGHI $.
    $( [18-Aug-2004] $) $( [17-Aug-2004] $)

  $( Intersection with class difference.  Theorem 34 of [Suppes] p. 29. $)
  indif $p |- ( A i^i ( A \ B ) ) = ( A \ B ) $=
    ( cdif cin dfin4 difeq2i difin 3eqtr2 ) AABCZDAAICZCAABDZCIAIEKJAABEFABGH
    $.
    $( [3-Sep-2004] $) $( [17-Aug-2004] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Distributive law for intersection over union.  Exercise 10 of
       [TakeutiZaring] p. 17. $)
    indi $p |- ( A i^i ( B u. C ) ) = ( ( A i^i B ) u. ( A i^i C ) ) $=
      ( vx cun cin cv wcel wa wo andi elun anbi2i elin orbi12i 3bitr4 eqriv )
      DABCEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJI
      ZUDUKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $( [30-Sep-2002] $)

    $( Distributive law for union over intersection.  Exercise 11 of
       [TakeutiZaring] p. 17. $)
    undi $p |- ( A u. ( B i^i C ) ) = ( ( A u. B ) i^i ( A u. C ) ) $=
      ( vx cin cun cv wcel wo wa ordi elin orbi2i elun anbi12i 3bitr4 eqriv )
      DABCEZFZABFZACFZEZDGZAHZUCRHZIZUCTHZUCUAHZJZUCSHUCUBHUDUCBHZUCCHZJZIUDUJI
      ZUDUKIZJUFUIUDUJUKKUEULUDUCBCLMUGUMUHUNUCABNUCACNOPUCARNUCTUALPQ $.
      $( [30-Sep-2002] $)
  $}

  $( Distributive law for intersection over union.  Theorem 28 of [Suppes]
     p. 27. $)
  indir $p |- ( ( A u. B ) i^i C ) = ( ( A i^i C ) u. ( B i^i C ) ) $=
    ( cun cin indi incom uneq12i 3eqtr4 ) CABDZECAEZCBEZDJCEACEZBCEZDCABFJCGMKN
    LACGBCGHI $.
    $( [30-Sep-2002] $)

  $( Distributive law for union over intersection.  Theorem 29 of [Suppes]
     p. 27. $)
  undir $p |- ( ( A i^i B ) u. C ) = ( ( A u. C ) i^i ( B u. C ) ) $=
    ( cin cun undi uncom ineq12i 3eqtr4 ) CABDZECAEZCBEZDJCEACEZBCEZDCABFJCGMKN
    LACGBCGHI $.
    $( [30-Sep-2002] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Infer equality from equalities of union and intersection.  Exercise 20
       of [Enderton] p. 32 and its converse. $)
    unineq $p |- ( ( ( A u. C ) = ( B u. C ) /\ ( A i^i C ) = ( B i^i C ) )
                 <-> A = B ) $=
      ( vx cun wceq cin wa cv wcel wb wi iba bibi12d eleq2 elin 3bitr3g
      syl5bir adantld wn wo biorf uncom eqeq12i sylbi elun adantrd pm2.61i
      eqrdv uneq1 ineq1 jca impbi ) ACEZBCEZFZACGZBCGZFZHZABFZUTDABDIZCJZUTVBAJ
      ZVBBJZKZLVCUSVFUPVCVFVDVCHZVEVCHZKUSVCVDVGVEVHVCVDMVCVEMNUSVBUQJVBURJVGVH
      UQURVBOVBACPVBBCPQRSVCTZUPVFUSVIVFVCVDUAZVCVEUAZKUPVIVDVJVEVKVCVDUBVCVEUB
      NUPVBCAEZJZVBCBEZJZVJVKUPVLVNFVMVOKUNVLUOVNACUCBCUCUDVLVNVBOUEVBCAUFVBCBU
      FQRUGUHUIVAUPUSABCUJABCUKULUM $.
      $( [25-Sep-2004] $) $( [10-Aug-2004] $)
  $}

  $( Equality of union and intersection implies equality of their arguments. $)
  uneqin $p |- ( ( A u. B ) = ( A i^i B ) <-> A = B ) $=
    ( cun cin wceq wss eqimss wa unss ssin sstr sylbir pm3.26 anim12i eqss
    sylibr syl uneq2 ineq2 inidm syl5eqr unidm syl5eq eqtr3d impbi ) ABCZABDZEZ
    ABEZUHUFUGFZUIUFUGGUJABFZBAFZHZUIUJAUGFZBUGFZHUMABUGIUNUKUOULUNAAFUKHUKAABJ
    AABKLUOULBBFZHULBABJULUPMLNLABOPQUIAACZUFUGABARUIAUGUQUIAADUGAABASATUAAUBUC
    UDUE $.
    $( [17-Apr-2006] $) $( [16-Apr-2006] $)

  $( Distributive law for class difference.  Theorem 39 of [Suppes] p. 29. $)
  difundi $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) i^i ( A \ C ) ) $=
    ( cun cdif cvv cin dfun3 difeq2i inindi dfin2 invdif ineq12i 3eqtr3 eqtr )
    ABCDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASGAQGZARGZGUAUDAQRJASKUEUBUFUCABLAC
    LMNO $.
    $( [31-Aug-2004] $) $( [17-Aug-2004] $)

  $( Distributive law for class difference. $)
  difundir $p |- ( ( A u. B ) \ C ) = ( ( A \ C ) u. ( B \ C ) ) $=
    ( cun cvv cdif cin indir invdif uneq12i 3eqtr3 ) ABDZECFZGAMGZBMGZDLCFACFZB
    CFZDABMHLCINPOQACIBCIJK $.
    $( [18-Aug-2004] $) $( [17-Aug-2004] $)

  $( Distributive law for class difference.  Theorem 40 of [Suppes] p. 29. $)
  difindi $p |- ( A \ ( B i^i C ) ) = ( ( A \ B ) u. ( A \ C ) ) $=
    ( cin cdif cvv cun dfin3 difeq2i indi dfin2 invdif uneq12i 3eqtr3 eqtr ) AB
    CDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASDAQDZARDZGUAUDAQRJASKUEUBUFUCABLACLM
    NO $.
    $( [18-Sep-2004] $) $( [17-Aug-2004] $)

  $( Distributive law for class difference. $)
  difindir $p |- ( ( A i^i B ) \ C ) = ( ( A \ C ) i^i ( B \ C ) ) $=
    ( cin cvv cdif inindir invdif ineq12i 3eqtr3 ) ABDZECFZDALDZBLDZDKCFACFZBCF
    ZDABLGKCHMONPACHBCHIJ $.
    $( [19-Sep-2004] $) $( [17-Aug-2004] $)

  $( DeMorgan's law for union.  Theorem 5.2(13) of [Stoll] p. 19. $)
  undm $p |- ( V \ ( A u. B ) ) = ( ( V \ A ) i^i ( V \ B ) ) $=
    ( cvv difundi ) CABD $.
    $( [31-Aug-2004] $) $( [18-Aug-2004] $)

  $( DeMorgan's law for intersection.  Theorem 5.2(13') of [Stoll] p. 19. $)
  indm $p |- ( V \ ( A i^i B ) ) = ( ( V \ A ) u. ( V \ B ) ) $=
    ( cvv difindi ) CABD $.
    $( [20-Sep-2004] $) $( [18-Aug-2004] $)

  $( A relationship involving double difference and union. $)
  difun1 $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) \ C ) $=
    ( cvv cdif cin cun inass invdif undm ineq2i eqtr3 3eqtr3 difeq1i ) ADBEZFZC
    EZABCGZEZABEZCEPDCEZFAOUAFZFZQSAOUAHPCIADREZFUCSUDUBABCJKARILMPTCABINL $.
    $( [4-Sep-2004] $) $( [29-Aug-2004] $)

  $( Swap second and third argument of double difference. $)
  dif23 $p |- ( ( A \ B ) \ C ) = ( ( A \ C ) \ B ) $=
    ( cun cdif uncom difeq2i difun1 3eqtr3 ) ABCDZEACBDZEABECEACEBEJKABCFGABCHA
    CBHI $.
    $( [27-Aug-2004] $) $( [18-Aug-2004] $)

  $( Two ways to express symmetric difference.  This theorem shows the
     equivalence of the definition of symmetric difference in [Stoll] p. 13
     and the restated definition in Example 4.1 of [Stoll] p. 262. $)
  symdif1 $p |- ( ( A \ B ) u. ( B \ A ) ) = ( ( A u. B ) \ ( A i^i B ) ) $=
    ( cun cin cdif difundir difin incom difeq2i eqtr uneq12i eqtr2 ) ABCABDZEAM
    EZBMEZCABEZBAEZCABMFNPOQABGOBBADZEQMRBABHIBAGJKL $.
    $( [2-Sep-2004] $) $( [17-Aug-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express symmetric difference. $)
    symdif2 $p |- ( ( A \ B ) u. ( B \ A ) ) =
                  { x | -. ( x e. A <-> x e. B ) } $=
      ( cv wcel wb wn cdif cun wo wa elun eldif pm4.13 anbi1i bitr ancom
      orbi12i orcom dfbi nbbn 3bitr2 3bitr abbi2i ) ADZBEZUECEZFGZABCHZCBHZIZUE
      UKEUEUIEZUEUJEZJUFGZGZUGGZKZUNUGKZJZUHUEUIUJLULUQUMURULUFUPKUQUEBCMUFUOUP
      UFNOPUMUGUNKURUECBMUGUNQPRUSURUQJUNUGFUHUQURSUNUGTUFUGUAUBUCUD $.
      $( [21-Sep-2004] $) $( [17-Aug-2004] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Union of two class abstractions. $)
    unab $p |- ( { x | ph } u. { x | ps } ) = { x | ( ph \/ ps ) } $=
      ( vy cab cun wo cv wcel wsbc df-clab orbi12i sbor bitr4 elun 3bitr4
      eqriv ) DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACUCJZUCTIUCUBIUFACUCJZBCUCJZGUGU
      DUHUEUIADCKBDCKLABCDMNUCRSOUADCKPQ $.
      $( [29-Sep-2002] $)

    $( Intersection of two class abstractions. $)
    inab $p |- ( { x | ph } i^i { x | ps } ) = { x | ( ph /\ ps ) } $=
      ( vy cab cin wa cv wcel wsbc df-clab anbi12i sban bitr4 elin 3bitr4
      eqriv ) DACEZBCEZFZABGZCEZDHZRIZUCSIZGZUACUCJZUCTIUCUBIUFACUCJZBCUCJZGUGU
      DUHUEUIADCKBDCKLABCDMNUCRSOUADCKPQ $.
      $( [29-Sep-2002] $)

    $( Difference of two class abstractions. $)
    difab $p |- ( { x | ph } \ { x | ps } ) = { x | ( ph /\ -. ps ) } $=
      ( vy cab cvv cdif cin wn wa cv wcel wsbc sbn df-clab negbii 3bitr4
      visset biantrur bitr2 difeqri ineq2i invdif inab 3eqtr3 ) ACEZFBCEZGZHUFB
      IZCEZHUFUGGAUIJCEUHUJUFDFUGUJDKZUJLZUKUGLZIZUKFLZUNJUICUKMBCUKMZIULUNBCDN
      UIDCOUMUPBDCOPQUOUNDRSTUAUBUFUGUCAUICUDUE $.
      $( [23-Oct-2004] $) $( [23-Oct-2004] $)
  $}

  $( Union of two restricted class abstractions. $)
  unrab $p |- ( { x e. A | ph } u. { x e. A | ps } ) =
              { x e. A | ( ph \/ ps ) } $=
    ( cv wcel wa cab cun wo crab unab andi abbii eqtr4 df-rab uneq12i 3eqtr4 )
    CEDFZAGZCHZSBGZCHZIZSABJZGZCHZACDKZBCDKZIUECDKUDTUBJZCHUGTUBCLUFUJCSABMNOUH
    UAUIUCACDPBCDPQUECDPR $.
    $( [26-Mar-2004] $) $( [25-Mar-2004] $)

  $( Intersection of two restricted class abstractions. $)
  inrab $p |- ( { x e. A | ph } i^i { x e. A | ps } ) =
              { x e. A | ( ph /\ ps ) } $=
    ( cv wcel wa cab cin crab inab anandi abbii eqtr4 df-rab ineq12i 3eqtr4 )
    CEDFZAGZCHZRBGZCHZIZRABGZGZCHZACDJZBCDJZIUDCDJUCSUAGZCHUFSUACKUEUICRABLMNUG
    TUHUBACDOBCDOPUDCDOQ $.
    $( [5-Sep-2006] $) $( [1-Sep-2006] $)

  ${
    $d x B $.
    $( Intersection with a restricted class abstraction. $)
    inrab2 $p |- ( { x e. A | ph } i^i B ) = { x e. ( A i^i B ) | ph } $=
      ( cv wcel wa cab cin crab inab elin anbi1i an23 bitr abbii eqtr4 df-rab
      abid2 eqcomi ineq12i 3eqtr4 ) BEZCFZAGZBHZUCDFZBHZIZUCCDIZFZAGZBHZABCJZDI
      ABUJJUIUEUGGZBHUMUEUGBKULUOBULUDUGGZAGUOUKUPAUCCDLMUDUGANOPQUNUFDUHABCRUH
      DBDSTUAABUJRUB $.
      $( [22-Nov-2007] $) $( [19-Nov-2007] $)
  $}

  $( Difference of two restricted class abstractions. $)
  difrab $p |- ( { x e. A | ph } \ { x e. A | ps } ) =
              { x e. A | ( ph /\ -. ps ) } $=
    ( cv wcel wa cab cdif wn crab difab anass pm3.27 con3i anim2i wi pm3.2
    adantr con3d imdistani impbi bitr3 abbii eqtr4 df-rab difeq12i 3eqtr4 ) CED
    FZAGZCHZUIBGZCHZIZUIABJZGZGZCHZACDKZBCDKZIUPCDKUNUJULJZGZCHURUJULCLUQVBCUQU
    JUOGZVBUIAUOMVCVBUOVAUJULBUIBNOPUJVAUOUJBULUIBULQAUIBRSTUAUBUCUDUEUSUKUTUMA
    CDUFBCDUFUGUPCDUFUH $.
    $( [23-Oct-2004] $) $( [23-Oct-2004] $)

  ${
    $d x A $.
    $( Alternate definition of restricted class abstraction. $)
    dfrab2 $p |- { x e. A | ph } = ( { x | ph } i^i A ) $=
      ( crab cv wcel wa cab cin df-rab inab abid2 ineq1i eqtr3 incom 3eqtr ) AB
      CDBECFZAGBHZCABHZIZSCIABCJQBHZSIRTQABKUACSBCLMNCSOP $.
      $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Transfer uniqueness to a smaller subclass. $)
    reuss2 $p |- ( ( ( A (_ B /\ A. x e. A ( ph -> ps ) ) /\
                 ( E. x e. A ph /\ E! x e. B ps ) ) -> E! x e. A ph ) $=
      ( wss wi wral wa cv wcel wex weu wreu wrex wmo wal prth ssel sylan exp4b
      com23 a2d imp4a 19.20dv imp df-ral sylan2b euimmo syl eu5 biimpr ex syl9
      imp32 df-reu sylibr df-rex anbi12i ) DEFZABGZCDHZIZCJZDKZAIZCLZVDEKZBIZCM
      ZIZACDNZACDOZBCENZIVCVKIVFCMZVLVCVGVJVOVCVJVFCPZVGVOVCVFVIGZCQZVJVPGUTVEV
      AGZCQZVRVBUTVTVRUTVSVQCUTVSVEAVIUTVEVAAVIGZUTVAVEWAUTVAVEAVIVEVHGVAVQUTVE
      VHABRDEVDSTUAUBUCUDUEUFVACDUGUHVFVICUIUJVGVPVOVOVGVPIVFCUKULUMUNUOACDUPUQ
      VMVGVNVJACDURBCEUPUSUH $.
      $( [26-Oct-2005] $) $( [20-Oct-2005] $)

    $( Transfer uniqueness to a smaller subclass. $)
    reuss $p |- ( ( A (_ B /\ E. x e. A ph /\ E! x e. B ph ) ->
                E! x e. A ph ) $=
      ( wss wrex wreu wi wral wa cv wcel idd rgen reuss2 mpanl2 3impb ) CDEZABC
      FZABDGZABCGZRAAHZBCISTJUAUBBCBKCLAMNAABCDOPQ $.
      $( [2-Nov-2005] $) $( [21-Aug-1999] $)
$(
      ( wss wrex wreu cv wcel wa wex weu wmo wi wal ssel anim1d 19.21aiv
      euimmo syl eu5 biimpr ex syl9 imp32 df-reu sylibr df-rex anbi12i
      sylan2b 3impb ) CDEZABCFZABDGZABCGZULBHZCIZAJZBKZUPDIZAJZBLZJZUOUMUNJULVC
      JURBLZUOULUSVBVDULVBURBMZUSVDULURVANZBOVBVENULVFBULUQUTACDUPPQRURVABSTUSV
      EVDVDUSVEJURBUAUBUCUDUEABCUFUGUMUSUNVBABCUHABDUFUIUJUK $.
$)

    $( Transfer uniqueness to a smaller class. $)
    reuun1 $p |- ( ( E. x e. A ph /\ E! x e. ( A u. B ) ( ph \/ ps ) )
                 -> E! x e. A ph ) $=
      ( cun wss wo wi wral wrex wreu wa ssun1 cv wcel orc a1i rgen reuss2
      mpanl12 ) DDEFZGAABHZIZCDJACDKUCCUBLMACDLDENUDCDUDCODPABQRSAUCCDUBTUA $.
      $( [10-Nov-2005] $) $( [21-Oct-2005] $)

    $( Transfer uniqueness to a smaller or larger class. $)
    reuun2 $p |- ( -. E. x e. B ph ->
             ( E! x e. ( A u. B ) ph <-> E! x e. A ph ) ) $=
      ( wrex wn cv wcel wa wo weu cun wreu wex wb df-rex negbii euor2 sylbi
      df-reu elun anbi1i andir orcom 3bitr eubii bitr 3bitr4g ) ABDEZFZBGZDHZAI
      ZUKCHZAIZJZBKZUOBKZABCDLZMZABCMUJUMBNZFUQUROUIVAABDPQUMUOBRSUTUKUSHZAIZBK
      UQABUSTVCUPBVCUNULJZAIUOUMJUPVBVDAUKCDUAUBUNULAUCUOUMUDUEUFUGABCTUH $.
      $( [28-Oct-2005] $) $( [21-Oct-2005] $)

    $( Restricted uniqueness "picks" a member of a subclass. $)
    reupick $p |- ( ( ( A (_ B /\ ( E. x e. A ph /\ E! x e. B ph ) ) /\ ph ) ->
                  ( x e. A <-> x e. B ) ) $=
      ( wss wrex wreu wa cv wcel wi ssel ad2antrr wex weu ancrd anim1d an23
      syl6ib 19.22dv eupick ex syl9 com23 imp32 df-rex df-reu anbi12i sylan2b
      exp3a imp impbid ) CDEZABCFZABDGZHZHZAHBIZCJZURDJZUMUSUTKUPACDURLZMUQAUTU
      SKUQUTAUSUQUTAUSUMUSAHZBNZUTAHZBOZHVDUSKZUPUMVCVEVFUMVEVCVFUMVCVDUSHZBNZV
      EVFUMVBVGBUMVBUTUSHZAHVGUMUSVIAUMUSUTVAPQUTUSARSTVEVHVFVDUSBUAUBUCUDUEUNV
      CUOVEABCUFABDUGUHUIUJUDUKUL $.
      $( [21-Aug-1999] $)
  $}

