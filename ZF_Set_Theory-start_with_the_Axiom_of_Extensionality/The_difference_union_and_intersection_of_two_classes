$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The difference, union, and intersection of two classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for class difference.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    difeq1 $p |- ( A = B -> ( A \ C ) = ( B \ C ) ) $=
      ( vx wceq cv wcel wn crab cdif rabeq dfdif2 3eqtr4g ) ABEDFCGHZDAINDBIACJ
      BCJNDABKDACLDBCLM $.
      $( [26-Jun-2011] $) $( [10-Feb-1997] $)

    $( Equality theorem for class difference.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    difeq2 $p |- ( A = B -> ( C \ A ) = ( C \ B ) ) $=
      ( vx wceq cv wcel wn crab cdif eleq2 notbid rabbidv dfdif2 3eqtr4g ) ABEZ
      DFZAGZHZDCIQBGZHZDCICAJCBJPSUADCPRTABQKLMDCANDCBNO $.
      $( [26-Jun-2011] $) $( [10-Feb-1997] $)
  $}

  $( Equality theorem for class difference.  (Contributed by FL,
     31-Aug-2009.) $)
  difeq12 $p |- ( ( A = B /\ C = D ) -> ( A \ C ) = ( B \ D ) ) $=
    ( wceq cdif difeq1 difeq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [31-Aug-2009] $)

  ${
    difeq1i.1 $e |- A = B $.
    $( Inference adding difference to the right in a class equality. $)
    difeq1i $p |- ( A \ C ) = ( B \ C ) $=
      ( wceq cdif difeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [15-Nov-2002] $)

    $( Inference adding difference to the left in a class equality. $)
    difeq2i $p |- ( C \ A ) = ( C \ B ) $=
      ( wceq cdif difeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [15-Nov-2002] $)

    ${
      difeq12i.2 $e |- C = D $.
      $( Equality inference for class difference. $)
      difeq12i $p |- ( A \ C ) = ( B \ D ) $=
        ( cdif difeq1i difeq2i eqtri ) ACGBCGBDGABCEHCDBFIJ $.
        $( [29-Aug-2004] $)
    $}
  $}

  ${
    difeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding difference to the right in a class equality. $)
    difeq1d $p |- ( ph -> ( A \ C ) = ( B \ C ) ) $=
      ( wceq cdif difeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [15-Nov-2002] $)

    $( Deduction adding difference to the left in a class equality. $)
    difeq2d $p |- ( ph -> ( C \ A ) = ( C \ B ) ) $=
      ( wceq cdif difeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [15-Nov-2002] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    difeqri.1 $e |- ( ( x e. A /\ -. x e. B ) <-> x e. C ) $.
    $( Inference from membership to difference.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    difeqri $p |- ( A \ B ) = C $=
      ( cdif cv wcel wn wa eldif bitri eqriv ) ABCFZDAGZNHOBHOCHIJODHOBCKELM $.
      $( [26-Jun-2011] $) $( [17-May-1998] $)
  $}

  ${
    hbdif.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbdif.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for class difference.  (Unnecessary
       distinct variable restrictions were removed by David Abernethy,
       2-Apr-2010.) $)
    hbdif $p |- ( y e. ( A \ B ) -> A. x y e. ( A \ B ) ) $=
      ( cv cdif wcel wn wa eldif hbn hban hbxfrbi ) BGZCDHIPCIZPDIZJZKAPCDLQSAE
      RAFMNO $.
      $( [15-Apr-2010] $) $( [3-Dec-2003] $)
  $}

  $( Implication of membership in a class difference. $)
  eldifi $p |- ( A e. ( B \ C ) -> A e. B ) $=
    ( cdif wcel wn eldif simplbi ) ABCDEABEACEFABCGH $.
    $( [29-Apr-1994] $)

  $( Implication of membership in a class difference. $)
  eldifn $p |- ( A e. ( B \ C ) -> -. A e. C ) $=
    ( cdif wcel wn eldif simprbi ) ABCDEABEACEFABCGH $.
    $( [3-May-1994] $)

  $( A set does not belong to a class excluding it. $)
  elndif $p |- ( A e. B -> -. A e. ( C \ B ) ) $=
    ( cdif wcel eldifn con2i ) ACBDEABEACBFG $.
    $( [27-Jun-1994] $)

  $( Implication of membership in a class difference. $)
  neldif $p |- ( ( A e. B /\ -. A e. ( B \ C ) ) -> A e. C ) $=
    ( wcel cdif wn eldif simplbi2 con1d imp ) ABDZABCEDZFACDZKMLLKMFABCGHIJ $.
    $( [28-Jun-1994] $)

  ${
    $d x A $.  $d x B $.
    $( Double class difference.  Exercise 11 of [TakeutiZaring] p. 22. $)
    difdif $p |- ( A \ ( B \ A ) ) = A $=
      ( vx cdif cv wcel wn wa wi eldif notbii iman bitr4i anbi2i pm4.45im
      difeqri ) CABADZACEZAFZRQFZGZHSRBFZSIZHSUAUCSUAUBSGHZGUCTUDRBAJKUBSLMNSUB
      OMP $.
      $( [17-May-1998] $)

    $( Subclass relationship for class difference.  Exercise 14 of
       [TakeutiZaring] p. 22. $)
    difss $p |- ( A \ B ) C_ A $=
      ( vx cdif cv eldifi ssriv ) CABDACEABFG $.
      $( [29-Apr-1994] $)
  $}

  $( Preservation of a subclass relationship by class difference. $)
  ssdifss $p |- ( A C_ B -> ( A \ C ) C_ B ) $=
    ( cdif wss difss sstr mpan ) ACDZAEABEIBEACFIABGH $.
    $( [15-Feb-2007] $)

  ${
    $d x A $.
    $( Double complement under universal class.  Exercise 4.10(s) of
       [Mendelson] p. 231. $)
    ddif $p |- ( _V \ ( _V \ A ) ) = A $=
      ( vx cvv cdif cv wcel wn wa eldif visset mpbiran con2bii biantrur bitr2i
      difeqri ) BCCADZABEZAFZQPFZGZQCFZTHSRSUARGQCAIBJZKLUATUBMNO $.
      $( [8-Jan-2002] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Contraposition law for subsets. $)
    ssconb $p |- ( ( A C_ C /\ B C_ C ) ->
                  ( A C_ ( C \ B ) <-> B C_ ( C \ A ) ) ) $=
      ( vx wss wa cv wcel cdif wi wal wn wb ssel pm5.1 syl2an con2b a1i
      anbi12d jcab 3bitr4g eldif imbi2i albidv dfss2 ) ACEZBCEZFZDGZAHZUICBIZHZ
      JZDKUIBHZUICAIZHZJZDKAUKEBUOEUHUMUQDUHUJUICHZUNLZFZJZUNURUJLZFZJZUMUQUHUJ
      URJZUJUSJZFUNURJZUNVBJZFVAVDUHVEVGVFVHUFVEVGVEVGMUGACUINBCUINVEVGOPVFVHMU
      HUJUNQRSUJURUSTUNURVBTUAULUTUJUICBUBUCUPVCUNUICAUBUCUAUDDAUKUEDBUOUEUA $.
      $( [22-Mar-1998] $)

    $( Contraposition law for subsets.  Exercise 15 of [TakeutiZaring]
       p. 22. $)
    sscon $p |- ( A C_ B -> ( C \ B ) C_ ( C \ A ) ) $=
      ( vx wss cdif cv wcel wn wa ssel con3d anim2d eldif 3imtr4g ssrdv ) ABEZD
      CBFZCAFZQDGZCHZTBHZIZJUATAHZIZJTRHTSHQUCUEUAQUDUBABTKLMTCBNTCANOP $.
      $( [22-Mar-1998] $)

    $( Difference law for subsets. $)
    ssdif $p |- ( A C_ B -> ( A \ C ) C_ ( B \ C ) ) $=
      ( vx wss cdif cv wcel wn wa ssel anim1d eldif 3imtr4g ssrdv ) ABEZDACFZBC
      FZPDGZAHZSCHIZJSBHZUAJSQHSRHPTUBUAABSKLSACMSBCMNO $.
      $( [28-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in class union.  Theorem 12 of [Suppes]
       p. 25. $)
    elun $p |- ( A e. ( B u. C ) <-> ( A e. B \/ A e. C ) ) $=
      ( vx cun wcel cvv wo elisset jaoi cv wceq eleq1 orbi12d df-un elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARITSUAABIACIJDKZBFZUCCFZHUBDARGUCALUDTUE
      UAUCABMUCACMNDBCOPQ $.
      $( [7-Aug-1994] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    uneqri.1 $e |- ( ( x e. A \/ x e. B ) <-> x e. C ) $.
    $( Inference from membership to union. $)
    uneqri $p |- ( A u. B ) = C $=
      ( cun cv wcel wo elun bitri eqriv ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for union of classes.  Theorem 23 of [Suppes] p. 27. $)
    unidm $p |- ( A u. A ) = A $=
      ( vx cv wcel oridm uneqri ) BAAABCADEF $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Commutative law for union of classes.  Exercise 6 of [TakeutiZaring]
       p. 17.  (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
    uncom $p |- ( A u. B ) = ( B u. A ) $=
      ( vx cun cv wcel wo orcom elun bitr4i uneqri ) CABBADZCEZAFZMBFZGONGMLFNO
      HMBAIJK $.
      $( [26-Jun-2011] $) $( [25-Jun-1998] $)
  $}

  ${
    $( If a class equals the union of two other classes, then it equals the
       union of those two classes commuted. ~ equncom was automatically derived
       from ~ equncomVD using the tools program
       translate_without_overwriting.cmd and minimizing.  (Contributed by Alan
       Sare, 18-Feb-2012.) $)
    equncom $p |- ( A = ( B u. C ) <-> A = ( C u. B ) ) $=
      ( cun uncom eqeq2i ) BCDCBDABCEF $.
      $( [18-Feb-2012] $)
  $}

  ${
    equncomi.1 $e |- A = ( B u. C ) $.
    $( Inference form of ~ equncom . ~ equncomi was automatically derived from
       ~ equncomiVD using the tools program translate_without_overwriting.cmd
       and minimizing.  (Contributed by Alan Sare, 18-Feb-2012.) $)
    equncomi $p |- A = ( C u. B ) $=
      ( cun wceq equncom mpbi ) ABCEFACBEFDABCGH $.
      $( [18-Feb-2012] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for union of two classes. $)
    uneq1 $p |- ( A = B -> ( A u. C ) = ( B u. C ) ) $=
      ( vx wceq cun cv wcel wo eleq2 orbi1d elun 3bitr4g eqrdv ) ABEZDACFZBCFZO
      DGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [5-Aug-1993] $)
  $}

  $( Equality theorem for the union of two classes. $)
  uneq2 $p |- ( A = B -> ( C u. A ) = ( C u. B ) ) $=
    ( wceq cun uneq1 uncom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [5-Aug-1993] $)

  $( Equality theorem for union of two classes. $)
  uneq12 $p |- ( ( A = B /\ C = D ) -> ( A u. C ) = ( B u. D ) ) $=
    ( wceq cun uneq1 uneq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [29-Mar-1998] $)

  ${
    uneq1i.1 $e |- A = B $.
    $( Inference adding union to the right in a class equality. $)
    uneq1i $p |- ( A u. C ) = ( B u. C ) $=
      ( wceq cun uneq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [30-Aug-1993] $)

    $( Inference adding union to the left in a class equality. $)
    uneq2i $p |- ( C u. A ) = ( C u. B ) $=
      ( wceq cun uneq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [30-Aug-1993] $)

    ${
      uneq12i.2 $e |- C = D $.
      $( Equality inference for union of two classes.  (The proof was shortened
         by Eric Schmidt, 26-Jan-2007.) $)
      uneq12i $p |- ( A u. C ) = ( B u. D ) $=
        ( wceq cun uneq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
        $( [27-Jan-2007] $) $( [12-Aug-2004] $)
    $}
  $}

  ${
    uneq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction adding union to the right in a class equality. $)
    uneq1d $p |- ( ph -> ( A u. C ) = ( B u. C ) ) $=
      ( wceq cun uneq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [29-Mar-1998] $)

    $( Deduction adding union to the left in a class equality. $)
    uneq2d $p |- ( ph -> ( C u. A ) = ( C u. B ) ) $=
      ( wceq cun uneq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [29-Mar-1998] $)

    ${
      uneq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for union of two classes.  (The proof was shortened
         by Andrew Salmon, 26-Jun-2011.) $)
      uneq12d $p |- ( ph -> ( A u. C ) = ( B u. D ) ) $=
        ( wceq cun uneq12 syl11anc ) ABCHDEHBDICEIHFGBCDEJK $.
        $( [26-Jun-2011] $) $( [29-Sep-2004] $)
    $}
  $}

  ${
    hbun.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbun.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the union of classes. $)
    hbun $p |- ( y e. ( A u. B ) -> A. x y e. ( A u. B ) ) $=
      ( cv cun wcel wo elun hbor hbxfrbi ) BGZCDHINCIZNDIZJANCDKOPAEFLM $.
      $( [15-Sep-2003] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Associative law for union of classes.  Exercise 8 of [TakeutiZaring]
       p. 17.  (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
    unass $p |- ( ( A u. B ) u. C ) = ( A u. ( B u. C ) ) $=
      ( vx cun cv wcel wo elun orbi2i orbi1i orass bitr2i 3bitrri uneqri ) DABE
      ZCABCEZEZDFZRGSAGZSQGZHTSBGZSCGZHZHZSPGZUCHZSAQIUAUDTSBCIJUGTUBHZUCHUEUFU
      HUCSABIKTUBUCLMNO $.
      $( [26-Jun-2011] $) $( [3-May-1994] $)
  $}

  $( A rearrangement of union. $)
  un12 $p |- ( A u. ( B u. C ) ) = ( B u. ( A u. C ) ) $=
    ( cun uncom uneq1i unass 3eqtr3i ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH
    $.
    $( [12-Aug-2004] $)

  $( A rearrangement of union.  (The proof was shortened by Andrew Salmon,
     26-Jun-2011.) $)
  un23 $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. B ) $=
    ( cun unass un12 uncom 3eqtri ) ABDCDABCDDBACDZDIBDABCEABCFBIGH $.
    $( [26-Jun-2011] $) $( [12-Aug-2004] $)

  $( A rearrangement of the union of 4 classes. $)
  un4 $p |- ( ( A u. B ) u. ( C u. D ) ) =
            ( ( A u. C ) u. ( B u. D ) ) $=
    ( cun un12 uneq2i unass 3eqtr4i ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHA
    CLHI $.
    $( [12-Aug-2004] $)

  $( Union distributes over itself. $)
  unundi $p |- ( A u. ( B u. C ) ) = ( ( A u. B ) u. ( A u. C ) ) $=
    ( cun unidm uneq1i un4 eqtr3i ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [17-Aug-2004] $)

  $( Union distributes over itself. $)
  unundir $p |- ( ( A u. B ) u. C ) = ( ( A u. C ) u. ( B u. C ) ) $=
    ( cun unidm uneq2i un4 eqtr3i ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [17-Aug-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Subclass relationship for union of classes.  Theorem 25 of [Suppes]
       p. 27. $)
    ssun1 $p |- A C_ ( A u. B ) $=
      ( vx cun cv wcel wo orc elun sylibr ssriv ) CAABDZCEZAFZNMBFZGMLFNOHMABIJ
      K $.
      $( [5-Aug-1993] $)
  $}

  $( Subclass relationship for union of classes. $)
  ssun2 $p |- A C_ ( B u. A ) $=
    ( cun ssun1 uncom sseqtri ) AABCBACABDABEF $.
    $( [30-Aug-1993] $)

  $( Subclass law for union of classes. $)
  ssun3 $p |- ( A C_ B -> A C_ ( B u. C ) ) $=
    ( wss cun ssun1 sstr2 mpi ) ABDBBCEZDAIDBCFABIGH $.
    $( [5-Aug-1993] $)

  $( Subclass law for union of classes. $)
  ssun4 $p |- ( A C_ B -> A C_ ( C u. B ) ) $=
    ( wss cun ssun2 sstr2 mpi ) ABDBCBEZDAIDBCFABIGH $.
    $( [14-Aug-1994] $)

  $( Membership law for union of classes. $)
  elun1 $p |- ( A e. B -> A e. ( B u. C ) ) $=
    ( cun ssun1 sseli ) BBCDABCEF $.
    $( [5-Aug-1993] $)

  $( Membership law for union of classes. $)
  elun2 $p |- ( A e. B -> A e. ( C u. B ) ) $=
    ( cun ssun2 sseli ) BCBDABCEF $.
    $( [30-Aug-1993] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass law for union of classes.  (The proof was shortened by Andrew
       Salmon, 26-Jun-2011.) $)
    unss1 $p |- ( A C_ B -> ( A u. C ) C_ ( B u. C ) ) $=
      ( vx wss cun cv wcel wo ssel orim1d elun 3imtr4g ssrdv ) ABEZDACFZBCFZODG
      ZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [26-Jun-2011] $) $( [14-Oct-1999] $)

    $( A relationship between subclass and union.  Theorem 26 of [Suppes]
       p. 27.  (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
    ssequn1 $p |- ( A C_ B <-> ( A u. B ) = B ) $=
      ( vx cv wcel wi wal cun wb wss wceq wo bicom pm4.72 elun bibi1i 3bitr4i
      albii dfss2 dfcleq ) CDZAEZUABEZFZCGUAABHZEZUCIZCGABJUEBKUDUGCUCUBUCLZIUH
      UCIUDUGUCUHMUBUCNUFUHUCUAABOPQRCABSCUEBTQ $.
      $( [26-Jun-2011] $) $( [30-Aug-1993] $)
  $}

  $( Subclass law for union of classes.  Exercise 7 of [TakeutiZaring]
     p. 18. $)
  unss2 $p |- ( A C_ B -> ( C u. A ) C_ ( C u. B ) ) $=
    ( wss cun unss1 uncom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [14-Oct-1999] $)

  $( Subclass law for union of classes. $)
  unss12 $p |- ( ( A C_ B /\ C C_ D ) -> ( A u. C ) C_ ( B u. D ) ) $=
    ( wss cun unss1 unss2 sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [2-Jun-2004] $)

  $( A relationship between subclass and union. $)
  ssequn2 $p |- ( A C_ B <-> ( B u. A ) = B ) $=
    ( wss cun wceq ssequn1 uncom eqeq1i bitri ) ABCABDZBEBADZBEABFJKBABGHI $.
    $( [23-Sep-2004] $) $( [13-Jun-1994] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( The union of two subclasses is a subclass.  Theorem 27 of [Suppes] p. 27
       and its converse. $)
    unss $p |- ( ( A C_ C /\ B C_ C ) <-> ( A u. B ) C_ C ) $=
      ( vx cv cun wcel wi wal wa wss wo elun imbi1i jaob bitri albii dfss2
      anbi12i 19.26 bitr4i 3bitr4ri ) DEZABFZGZUCCGZHZDIUCAGZUFHZUCBGZUFHZJZDIZ
      UDCKACKZBCKZJZUGULDUGUHUJLZUFHULUEUQUFUCABMNUHUFUJOPQDUDCRUPUIDIZUKDIZJUM
      UNURUOUSDACRDBCRSUIUKDTUAUB $.
      $( [11-Jun-2004] $)
  $}

  ${
    unssi.1 $e |- A C_ C $.
    unssi.2 $e |- B C_ C $.
    $( An inference that the union of two subclasses is a subclass.
       (Contributed by Raph Levien, 10-Dec-2002.) $)
    unssi $p |- ( A u. B ) C_ C $=
      ( wss wa cun pm3.2i unss mpbi ) ACFZBCFZGABHCFLMDEIABCJK $.
      $( [10-Dec-2002] $)
  $}

  $( A condition that implies inclusion in the union of two classes. $)
  ssun $p |- ( ( A C_ B \/ A C_ C ) -> A C_ ( B u. C ) ) $=
    ( wss cun ssun3 ssun4 jaoi ) ABDABCEDACDABCFACBGH $.
    $( [23-Nov-2003] $)

  $( Restricted existential quantification over union.  (Contributed by Jeff
     Madsen, 5-Jan-2011.) $)
  rexun $p |- ( E. x e. ( A u. B ) ph <->
                          ( E. x e. A ph \/ E. x e. B ph ) ) $=
    ( cun wrex cv wcel wa wex wo df-rex elun anbi1i andir bitri exbii 19.43
    orbi12i bitr4i 3bitri ) ABCDEZFBGZUBHZAIZBJUCCHZAIZUCDHZAIZKZBJZABCFZABDFZK
    ZABUBLUEUJBUEUFUHKZAIUJUDUOAUCCDMNUFUHAOPQUKUGBJZUIBJZKUNUGUIBRULUPUMUQABCL
    ABDLSTUA $.
    $( [29-Jan-2011] $) $( [5-Jan-2011] $)

  $( Restricted quantification over a union.  (Contributed by Scott Fenton,
     12-Apr-2011.)  (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
  ralunb $p |- ( A. x e. ( A u. B ) ph <->
                 ( A. x e. A ph /\ A. x e. B ph ) ) $=
    ( cv cun wcel wi wal wa wral wo elun imbi1i jaob bitri albii 19.26 df-ral
    anbi12i 3bitr4i ) BEZCDFZGZAHZBIZUBCGZAHZBIZUBDGZAHZBIZJZABUCKABCKZABDKZJUF
    UHUKJZBIUMUEUPBUEUGUJLZAHUPUDUQAUBCDMNUGAUJOPQUHUKBRPABUCSUNUIUOULABCSABDST
    UA $.
    $( [29-Jun-2011] $) $( [12-Apr-2011] $)

  $( Restricted quantification over union.  (Contributed by Jeff Madsen,
     2-Sep-2009.) $)
  ralun $p |- ( ( A. x e. A ph /\ A. x e. B ph ) -> A. x e. ( A u. B ) ph ) $=
    ( cun wral wa ralunb biimpri ) ABCDEFABCFABDFGABCDHI $.
    $( [2-Sep-2009] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in an intersection of two classes.  Theorem 12
       of [Suppes] p. 25. $)
    elin $p |- ( A e. ( B i^i C ) <-> ( A e. B /\ A e. C ) ) $=
      ( vx cin wcel cvv wa elisset adantl cv wceq eleq1 anbi12d df-in elab2g
      pm5.21nii ) ABCEZFAGFZABFZACFZHZARIUASTACIJDKZBFZUCCFZHUBDARGUCALUDTUEUAU
      CABMUCACMNDBCOPQ $.
      $( [29-Apr-1994] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Commutative law for intersection of classes.  Exercise 7 of
       [TakeutiZaring] p. 17. $)
    incom $p |- ( A i^i B ) = ( B i^i A ) $=
      ( vx cin cv wcel wa ancom elin 3bitr4i eqriv ) CABDZBADZCEZAFZNBFZGPOGNLF
      NMFOPHNABINBAIJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    ineqri.1 $e |- ( ( x e. A /\ x e. B ) <-> x e. C ) $.
    $( Inference from membership to intersection. $)
    ineqri $p |- ( A i^i B ) = C $=
      ( cin cv wcel wa elin bitri eqriv ) ABCFZDAGZMHNBHNCHINDHNBCJEKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Equality theorem for intersection of two classes. $)
    ineq1 $p |- ( A = B -> ( A i^i C ) = ( B i^i C ) ) $=
      ( vx wceq cin cv wcel wa eleq2 anbi1d elin 3bitr4g eqrdv ) ABEZDACFZBCFZO
      DGZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [14-Dec-1993] $)
  $}

  $( Equality theorem for intersection of two classes. $)
  ineq2 $p |- ( A = B -> ( C i^i A ) = ( C i^i B ) ) $=
    ( wceq cin ineq1 incom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [26-Dec-1993] $)

  $( Equality theorem for intersection of two classes. $)
  ineq12 $p |- ( ( A = B /\ C = D ) -> ( A i^i C ) = ( B i^i D ) ) $=
    ( wceq cin ineq1 ineq2 sylan9eq ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [8-May-1994] $)

  ${
    ineq1i.1 $e |- A = B $.
    $( Equality inference for intersection of two classes. $)
    ineq1i $p |- ( A i^i C ) = ( B i^i C ) $=
      ( wceq cin ineq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [26-Dec-1993] $)

    $( Equality inference for intersection of two classes. $)
    ineq2i $p |- ( C i^i A ) = ( C i^i B ) $=
      ( wceq cin ineq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [26-Dec-1993] $)

    ${
      ineq12i.2 $e |- C = D $.
      $( Equality inference for intersection of two classes.  (The proof was
         shortened by Eric Schmidt, 26-Jan-2007.) $)
      ineq12i $p |- ( A i^i C ) = ( B i^i D ) $=
        ( wceq cin ineq12 mp2an ) ABGCDGACHBDHGEFABCDIJ $.
        $( [27-Jan-2007] $) $( [24-Jun-2004] $)
    $}
  $}

  ${
    ineq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for intersection of two classes. $)
    ineq1d $p |- ( ph -> ( A i^i C ) = ( B i^i C ) ) $=
      ( wceq cin ineq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [10-Apr-1994] $)

    $( Equality deduction for intersection of two classes. $)
    ineq2d $p |- ( ph -> ( C i^i A ) = ( C i^i B ) ) $=
      ( wceq cin ineq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [10-Apr-1994] $)

    ${
      ineq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for intersection of two classes.  (The proof was
         shortened by Andrew Salmon, 26-Jun-2011.) $)
      ineq12d $p |- ( ph -> ( A i^i C ) = ( B i^i D ) ) $=
        ( wceq cin ineq12 syl11anc ) ABCHDEHBDICEIHFGBCDEJK $.
        $( [26-Jun-2011] $) $( [24-Jun-2004] $)
    $}

    ${
      ineqan12d.2 $e |- ( ps -> C = D ) $.
      $( Equality deduction for intersection of two classes. $)
      ineqan12d $p |- ( ( ph /\ ps ) -> ( A i^i C ) = ( B i^i D ) ) $=
        ( wceq cin ineq12 syl2an ) ACDIEFICEJDFJIBGHCDEFKL $.
        $( [7-Feb-2007] $)
    $}
  $}

  ${
    hbin.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbin.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for the intersection of classes.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 6-Apr-2010.) $)
    hbin $p |- ( y e. ( A i^i B ) -> A. x y e. ( A i^i B ) ) $=
      ( cv cin wcel wa elin hban hbxfrbi ) BGZCDHINCIZNDIZJANCDKOPAEFLM $.
      $( [15-Apr-2010] $) $( [15-Sep-2003] $)
  $}

  ${
    $d A y $.  $d C y z $.  $d D y z $.  $d x y z $.
    $( Distribute proper substitution through an intersection relation.
       (Contributed by Alan Sare, 22-Jul-2012.) $)
    csbing $p |- ( A e. B -> [_ A / x ]_ ( C i^i D ) =
                  ( [_ A / x ]_ C i^i [_ A / x ]_ D ) ) $=
      ( vy vz cv cin csb wceq csbeq1 ineq12d eqeq12d visset wcel ax-17 hbcsb1
      hbin csbeq1a csbief vtoclg ) AFHZDEIZJZAUCDJZAUCEJZIZKABUDJZABDJZABEJZIZK
      FBCUCBKZUEUIUHULAUCBUDLUMUFUJUGUKAUCBDLAUCBELMNAGUCUDUHFOZAGUFUGAGUCDUNGH
      UCPAQZRAGUCEUNUORSAHUCKDUFEUGAUCDTAUCETMUAUB $.
      $( [22-Jul-2012] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    rabbirdv.1 $e |- ( ph -> ( x e. B -> ( x e. A <-> ch ) ) ) $.
    $( Deduction from wff to restricted class abstraction.  (The proof was
       shortened by Andrew Salmon, 26-Jun-2011.) $)
    rabbirdv $p |- ( ph -> ( B i^i A ) = { x e. B | ch } ) $=
      ( cin cv wcel crab dfin5 wb imp rabbidva syl5eq ) AEDGCHZDIZCEJBCEJCEDKAQ
      BCEAPEIQBLFMNO $.
      $( [26-Jun-2011] $) $( [27-Aug-2004] $)
  $}

  ${
    $d x A $.
    $( Idempotent law for intersection of classes.  Theorem 15 of [Suppes]
       p. 26. $)
    inidm $p |- ( A i^i A ) = A $=
      ( vx cv wcel anidm ineqri ) BAAABCADEF $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Associative law for intersection of classes.  Exercise 9 of
       [TakeutiZaring] p. 17. $)
    inass $p |- ( ( A i^i B ) i^i C ) = ( A i^i ( B i^i C ) ) $=
      ( vx cin cv wcel wa anass elin anbi2i bitr4i anbi1i 3bitr4i ineqri ) DABE
      ZCABCEZEZDFZAGZSBGZHZSCGZHZTSQGZHZSPGZUCHSRGUDTUAUCHZHUFTUAUCIUEUHTSBCJKL
      UGUBUCSABJMSAQJNO $.
      $( [3-May-1994] $)
  $}

  $( A rearrangement of intersection. $)
  in12 $p |- ( A i^i ( B i^i C ) ) = ( B i^i ( A i^i C ) ) $=
    ( cin incom ineq1i inass 3eqtr3i ) ABDZCDBADZCDABCDDBACDDIJCABEFABCGBACGH
    $.
    $( [21-Apr-2001] $)

  $( A rearrangement of intersection.  (The proof was shortened by Andrew
     Salmon, 26-Jun-2011.) $)
  in32 $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i B ) $=
    ( cin inass in12 incom 3eqtri ) ABDCDABCDDBACDZDIBDABCEABCFBIGH $.
    $( [26-Jun-2011] $) $( [21-Apr-2001] $)

  $( A rearrangement of intersection. $)
  in13 $p |- ( A i^i ( B i^i C ) ) = ( C i^i ( B i^i A ) ) $=
    ( cin in32 incom 3eqtr4i ) BCDZADBADZCDAHDCIDBCAEAHFCIFG $.
    $( [27-Aug-2012] $)

  $( A rearrangement of intersection. $)
  in31 $p |- ( ( A i^i B ) i^i C ) = ( ( C i^i B ) i^i A ) $=
    ( cin in12 incom 3eqtr4i ) CABDZDACBDZDHCDIADCABEHCFIAFG $.
    $( [27-Aug-2012] $)

  $( Rotate the intersection of 3 classes.. $)
  inrot $p |- ( ( A i^i B ) i^i C ) = ( ( C i^i A ) i^i B ) $=
    ( cin in31 in32 eqtri ) ABDCDCBDADCADBDABCECBAFG $.
    $( [27-Aug-2012] $)

  $( Rearrangement of intersection of 4 classes. $)
  in4 $p |- ( ( A i^i B ) i^i ( C i^i D ) ) =
            ( ( A i^i C ) i^i ( B i^i D ) ) $=
    ( cin in12 ineq2i inass 3eqtr4i ) ABCDEZEZEACBDEZEZEABEJEACELEKMABCDFGABJHA
    CLHI $.
    $( [21-Apr-2001] $)

  $( Intersection distributes over itself. $)
  inindi $p |- ( A i^i ( B i^i C ) ) = ( ( A i^i B ) i^i ( A i^i C ) ) $=
    ( cin inidm ineq1i in4 eqtr3i ) AADZBCDZDAJDABDACDDIAJAEFAABCGH $.
    $( [6-May-1994] $)

  $( Intersection distributes over itself. $)
  inindir $p |- ( ( A i^i B ) i^i C ) = ( ( A i^i C ) i^i ( B i^i C ) ) $=
    ( cin inidm ineq2i in4 eqtr3i ) ABDZCCDZDICDACDBCDDJCICEFABCCGH $.
    $( [17-Aug-2004] $)

  $( A relationship between subclass and intersection.  Similar to Exercise 9
     of [TakeutiZaring] p. 18. $)
  sseqin2 $p |- ( A C_ B <-> ( B i^i A ) = A ) $=
    ( wss cin wceq df-ss incom eqeq1i bitri ) ABCABDZAEBADZAEABFJKAABGHI $.
    $( [23-Sep-2004] $) $( [17-May-1994] $)

  ${
    $d x A $.  $d x B $.
    $( The intersection of two classes is a subset of one of them.  Part of
       Exercise 12 of [TakeutiZaring] p. 18. $)
    inss1 $p |- ( A i^i B ) C_ A $=
      ( vx cin cv wcel elin simplbi ssriv ) CABDZACEZJFKAFKBFKABGHI $.
      $( [27-Apr-1994] $)
  $}

  $( The intersection of two classes is a subset of one of them.  Part of
     Exercise 12 of [TakeutiZaring] p. 18. $)
  inss2 $p |- ( A i^i B ) C_ B $=
    ( cin incom inss1 eqsstr3i ) ABCBACBBADBAEF $.
    $( [27-Apr-1994] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Subclass of intersection.  Theorem 2.8(vii) of [Monk1] p. 26.  (The
       proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
    ssin $p |- ( ( A C_ B /\ A C_ C ) <-> A C_ ( B i^i C ) ) $=
      ( vx cv wcel wi wal wa cin wss elin imbi2i albii jcab 19.26 3bitrri
      dfss2 anbi12i 3bitr4i ) DEZAFZUABFZGZDHZUBUACFZGZDHZIZUBUABCJZFZGZDHZABKZ
      ACKZIAUJKUMUBUCUFIZGZDHUDUGIZDHUIULUQDUKUPUBUABCLMNUQURDUBUCUFONUDUGDPQUN
      UEUOUHDABRDACRSDAUJRT $.
      $( [26-Jun-2011] $) $( [15-Jun-2004] $)
  $}

  ${
    ssini.1 $e |- A C_ B $.
    ssini.2 $e |- A C_ C $.
    $( An inference showing that the a subclass of two classes is a subclass of
       their intersection. $)
    ssini $p |- A C_ ( B i^i C ) $=
      ( wss wa cin pm3.2i ssin mpbi ) ABFZACFZGABCHFLMDEIABCJK $.
      $( [24-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Add right intersection to subclass relation.  (The proof was shortened
       by Andrew Salmon, 26-Jun-2011.) $)
    ssrin $p |- ( A C_ B -> ( A i^i C ) C_ ( B i^i C ) ) $=
      ( vx wss cin cv wcel wa ssel anim1d elin 3imtr4g ssrdv ) ABEZDACFZBCFZODG
      ZAHZRCHZIRBHZTIRPHRQHOSUATABRJKRACLRBCLMN $.
      $( [26-Jun-2011] $) $( [16-Aug-1994] $)

    $( Add left intersection to subclass relation. $)
    sslin $p |- ( A C_ B -> ( C i^i A ) C_ ( C i^i B ) ) $=
      ( wss cin ssrin incom 3sstr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
      $( [19-Oct-1999] $)
  $}

  $( Intersection of subclasses. $)
  ss2in $p |- ( ( A C_ B /\ C C_ D ) -> ( A i^i C ) C_ ( B i^i D ) ) $=
    ( wss cin ssrin sslin sylan9ss ) ABECDEACFBCFBDFABCGCDBHI $.
    $( [5-May-2000] $)

  $( Intersection preserves subclass relationship. $)
  ssinss1 $p |- ( A C_ C -> ( A i^i B ) C_ C ) $=
    ( cin wss wi inss1 sstr2 ax-mp ) ABDZAEACEJCEFABGJACHI $.
    $( [14-Sep-1999] $)

  $( Absorption law for union. $)
  unabs $p |- ( A u. ( A i^i B ) ) = A $=
    ( cin wss cun wceq inss1 ssequn2 mpbi ) ABCZADAJEAFABGJAHI $.
    $( [16-Apr-2006] $)

  $( Absorption law for intersection. $)
  inabs $p |- ( A i^i ( A u. B ) ) = A $=
    ( cun wss cin wceq ssun1 df-ss mpbi ) AABCZDAJEAFABGAJHI $.
    $( [16-Apr-2006] $)

  $( Negation of subclass expressed in terms of intersection and proper
     subclass.  (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
  nssinpss $p |- ( -. A C_ B <-> ( A i^i B ) C. A ) $=
    ( cin wne wss wa wn wpss inss1 biantrur df-ss necon3bbii df-pss 3bitr4i )
    ABCZADZOAEZPFABEZGOAHQPABIJROAABKLOAMN $.
    $( [26-Jun-2011] $) $( [30-Jun-2004] $)

  $( Negation of subclass expressed in terms of proper subclass and union. $)
  nsspssun $p |- ( -. A C_ B <-> B C. ( A u. B ) ) $=
    ( cun wss wn wa wpss ssun2 biantrur ssid biantru unss bitri notbii dfpss3
    3bitr4i ) ABCZBDZEZBQDZSFABDZEBQGTSBAHIUARUAUABBDZFRUBUABJKABBLMNBQOP $.
    $( [15-Sep-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Subclass defined in terms of class difference.  See comments under
       ~ dfun2 .  (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
    dfss4 $p |- ( A C_ B <-> ( B \ ( B \ A ) ) = A ) $=
      ( vx wss cin wceq cdif sseqin2 cv wcel wn wa eldif notbii anbi2i wi elin
      abai iman 3bitri bitr4i difeqri eqeq1i ) ABDBAEZAFBBAGZGZAFABHUFUDACBUEUD
      CIZBJZUGUEJZKZLUHUHUGAJZKLZKZLZUGUDJZUJUMUHUIULUGBAMNOUOUHUKLUHUHUKPZLUNU
      GBAQUHUKRUPUMUHUHUKSOTUAUBUCUA $.
      $( [26-Jun-2011] $) $( [22-Mar-1998] $)

    $( An alternate definition of the union of two classes in terms of class
       difference, requiring no dummy variables.  Along with ~ dfin2 and
       ~ dfss4 it shows we can express union, intersection, and subset directly
       in terms of the single "primitive" operation ` \ ` (class
       difference). $)
    dfun2 $p |- ( A u. B ) = ( _V \ ( ( _V \ A ) \ B ) ) $=
      ( vx cun cvv cdif cv wcel wo wn wa eldif visset mpbiran anbi1i ioran
      3bitr4i con2bii elun eqriv ) CABDZEEAFZBFZFZCGZAHZUEBHZIZUEUCHZJZUEUAHUEU
      DHZUIUHUEUBHZUGJZKUFJZUMKUIUHJULUNUMULUEEHZUNUEEALCMZNOUEUBBLUFUGPQRUEABS
      UKUOUJUEEUCLUPNQT $.
      $( [10-Jun-2004] $)

    $( An alternate definition of the intersection of two classes in terms of
       class difference, requiring no dummy variables.  See comments under
       ~ dfun2 .  Another version is given by ~ dfin4 . $)
    dfin2 $p |- ( A i^i B ) = ( A \ ( _V \ B ) ) $=
      ( vx cin cvv cdif cv wcel wa wn eldif visset mpbiran con2bii anbi2i elin
      3bitr4i eqriv ) CABDZAEBFZFZCGZAHZUBBHZIUCUBTHZJZIUBSHUBUAHUDUFUCUEUDUEUB
      EHUDJUBEBKCLMNOUBABPUBATKQR $.
      $( [10-Jun-2004] $)

    $( Difference with intersection.  Theorem 33 of [Suppes] p. 29.  (The proof
       was shortened by Andrew Salmon, 26-Jun-2011.) $)
    difin $p |- ( A \ ( A i^i B ) ) = ( A \ B ) $=
      ( vx cin cdif cv wcel wi wn wa pm4.61 anclb elin imbi2i iman 3bitr2i
      con2bii eldif 3bitr4i difeqri ) CAABDZABEZCFZAGZUCBGZHZIUDUEIJUDUCUAGZIJZ
      UCUBGUDUEKUFUHUFUDUDUEJZHUDUGHUHIUDUELUGUIUDUCABMNUDUGOPQUCABRST $.
      $( [26-Jun-2011] $) $( [31-Mar-1998] $)
  $}

  $( Union defined in terms of intersection (DeMorgan's law).  Definition of
     union in [Mendelson] p. 231. $)
  dfun3 $p |- ( A u. B ) = ( _V \ ( ( _V \ A ) i^i ( _V \ B ) ) ) $=
    ( cun cvv cdif cin dfun2 dfin2 ddif difeq2i eqtr2i eqtri ) ABCDDAEZBEZEDMDB
    EZFZEABGNPDPMDOEZENMOHQBMBIJKJL $.
    $( [8-Jan-2002] $)

  $( Intersection defined in terms of union (DeMorgan's law.  Similar to
     Exercise 4.10(n) of [Mendelson] p. 231. $)
  dfin3 $p |- ( A i^i B ) = ( _V \ ( ( _V \ A ) u. ( _V \ B ) ) ) $=
    ( cvv cdif cun cin ddif dfun2 difeq1i difeq2i eqtri dfin2 3eqtr4ri ) CCACBD
    ZDZDZDOCCADZNEZDABFOGRPCRCCQDZNDZDPQNHTOCSANAGIJKJABLM $.
    $( [8-Jan-2002] $)

  $( Alternate definition of the intersection of two classes.  Exercise 4.10(q)
     of [Mendelson] p. 231. $)
  dfin4 $p |- ( A i^i B ) = ( A \ ( A \ B ) ) $=
    ( cin cdif wss wceq inss1 dfss4 mpbi difin difeq2i eqtr3i ) AAABCZDZDZMAABD
    ZDMAEOMFABGMAHINPAABJKL $.
    $( [25-Nov-2003] $)

  $( Intersection with universal complement.  Remark in [Stoll] p. 20. $)
  invdif $p |- ( A i^i ( _V \ B ) ) = ( A \ B ) $=
    ( cvv cdif cin dfin2 ddif difeq2i eqtri ) ACBDZEACJDZDABDAJFKBABGHI $.
    $( [17-Aug-2004] $)

  $( Intersection with class difference.  Theorem 34 of [Suppes] p. 29. $)
  indif $p |- ( A i^i ( A \ B ) ) = ( A \ B ) $=
    ( cdif cin dfin4 difeq2i difin 3eqtr2i ) AABCZDAAICZCAABDZCIAIEKJAABEFABGH
    $.
    $( [17-Aug-2004] $)

  $( Bring an intersection in and out of a class difference.  (Contributed by
     Jeff Hankins, 15-Jul-2009.) $)
  indif2 $p |- ( A i^i ( B \ C ) ) = ( ( A i^i B ) \ C ) $=
    ( cin cvv cdif inass invdif ineq2i 3eqtr3ri ) ABDZECFZDABLDZDKCFABCFZDABLGK
    CHMNABCHIJ $.
    $( [12-Jul-2009] $)

  $( Commutation law for intersection and difference.  (Contributed by Scott
     Fenton, 18-Feb-2013.) $)
  indifcom $p |- ( A i^i ( B \ C ) ) = ( B i^i ( A \ C ) ) $=
    ( cin cdif incom difeq1i indif2 3eqtr4i ) ABDZCEBADZCEABCEDBACEDJKCABFGABCH
    BACHI $.
    $( [18-Feb-2013] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Distributive law for intersection over union.  Exercise 10 of
       [TakeutiZaring] p. 17.  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    indi $p |- ( A i^i ( B u. C ) ) = ( ( A i^i B ) u. ( A i^i C ) ) $=
      ( vx cun cin cv wcel wo wa andi elin orbi12i bitr4i elun anbi2i 3bitr4i
      ineqri ) DABCEZABFZACFZEZDGZAHZUCBHZUCCHZIZJZUCTHZUCUAHZIZUDUCSHZJUCUBHUH
      UDUEJZUDUFJZIUKUDUEUFKUIUMUJUNUCABLUCACLMNULUGUDUCBCOPUCTUAOQR $.
      $( [26-Jun-2011] $) $( [30-Sep-2002] $)

    $( Distributive law for union over intersection.  Exercise 11 of
       [TakeutiZaring] p. 17.  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    undi $p |- ( A u. ( B i^i C ) ) = ( ( A u. B ) i^i ( A u. C ) ) $=
      ( vx cin cun cv wcel wa wo elin elun anbi12i orbi2i ordi bitr2i 3bitrri
      uneqri ) DABCEZABFZACFZEZDGZUBHUCTHZUCUAHZIUCAHZUCBHZJZUFUCCHZJZIZUFUCSHZ
      JZUCTUAKUDUHUEUJUCABLUCACLMUMUFUGUIIZJUKULUNUFUCBCKNUFUGUIOPQR $.
      $( [30-Sep-2002] $)
  $}

  $( Distributive law for intersection over union.  Theorem 28 of [Suppes]
     p. 27. $)
  indir $p |- ( ( A u. B ) i^i C ) = ( ( A i^i C ) u. ( B i^i C ) ) $=
    ( cun cin indi incom uneq12i 3eqtr4i ) CABDZECAEZCBEZDJCEACEZBCEZDCABFJCGMK
    NLACGBCGHI $.
    $( [30-Sep-2002] $)

  $( Distributive law for union over intersection.  Theorem 29 of [Suppes]
     p. 27. $)
  undir $p |- ( ( A i^i B ) u. C ) = ( ( A u. C ) i^i ( B u. C ) ) $=
    ( cin cun undi uncom ineq12i 3eqtr4i ) CABDZECAEZCBEZDJCEACEZBCEZDCABFJCGMK
    NLACGBCGHI $.
    $( [30-Sep-2002] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Infer equality from equalities of union and intersection.  Exercise 20
       of [Enderton] p. 32 and its converse. $)
    unineq $p |- ( ( ( A u. C ) = ( B u. C ) /\ ( A i^i C ) = ( B i^i C ) )
                 <-> A = B ) $=
      ( vx cun wceq cin wa cv wcel wb wi eleq2 elin 3bitr3g iba bibi12d
      syl5ibr adantld wn wo uncom eqeq12i sylbi elun biorf adantrd pm2.61i
      eqrdv uneq1 ineq1 jca impbii ) ACEZBCEZFZACGZBCGZFZHZABFZUTDABDIZCJZUTVBA
      JZVBBJZKZLVCUSVFUPUSVFVCVDVCHZVEVCHZKUSVBUQJVBURJVGVHUQURVBMVBACNVBBCNOVC
      VDVGVEVHVCVDPVCVEPQRSVCTZUPVFUSUPVFVIVCVDUAZVCVEUAZKUPVBCAEZJZVBCBEZJZVJV
      KUPVLVNFVMVOKUNVLUOVNACUBBCUBUCVLVNVBMUDVBCAUEVBCBUEOVIVDVJVEVKVCVDUFVCVE
      UFQRUGUHUIVAUPUSABCUJABCUKULUM $.
      $( [10-Aug-2004] $)
  $}

  $( Equality of union and intersection implies equality of their arguments.
     (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
  uneqin $p |- ( ( A u. B ) = ( A i^i B ) <-> A = B ) $=
    ( cun cin wceq wss wa eqimss unss ssin sstr sylbir simpl anim12i syl eqss
    sylibr uneq2 unidm inidm eqtr4i ineq2 syl5eq eqtr3d impbii ) ABCZABDZEZABEZ
    UHABFZBAFZGZUIUHUFUGFZULUFUGHUMAUGFZBUGFZGULABUGIUNUJUOUKUNAAFUJGUJAABJAABK
    LUOUKBBFZGUKBABJUKUPMLNLOABPQUIAACZUFUGABARUIUQAADZUGUQAURASATUAABAUBUCUDUE
    $.
    $( [16-Apr-2006] $)

  $( Distributive law for class difference.  Theorem 39 of [Suppes] p. 29. $)
  difundi $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) i^i ( A \ C ) ) $=
    ( cun cdif cvv cin dfun3 difeq2i inindi dfin2 invdif ineq12i 3eqtr3i eqtri
    ) ABCDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASGAQGZARGZGUAUDAQRJASKUEUBUFUCABL
    ACLMNO $.
    $( [17-Aug-2004] $)

  $( Distributive law for class difference. $)
  difundir $p |- ( ( A u. B ) \ C ) = ( ( A \ C ) u. ( B \ C ) ) $=
    ( cun cvv cdif cin indir invdif uneq12i 3eqtr3i ) ABDZECFZGAMGZBMGZDLCFACFZ
    BCFZDABMHLCINPOQACIBCIJK $.
    $( [17-Aug-2004] $)

  $( Distributive law for class difference.  Theorem 40 of [Suppes] p. 29. $)
  difindi $p |- ( A \ ( B i^i C ) ) = ( ( A \ B ) u. ( A \ C ) ) $=
    ( cin cdif cvv cun dfin3 difeq2i indi dfin2 invdif uneq12i 3eqtr3i eqtri )
    ABCDZEAFFBEZFCEZGZEZEZABEZACEZGZPTABCHIASDAQDZARDZGUAUDAQRJASKUEUBUFUCABLAC
    LMNO $.
    $( [17-Aug-2004] $)

  $( Distributive law for class difference. $)
  difindir $p |- ( ( A i^i B ) \ C ) = ( ( A \ C ) i^i ( B \ C ) ) $=
    ( cin cvv cdif inindir invdif ineq12i 3eqtr3i ) ABDZECFZDALDZBLDZDKCFACFZBC
    FZDABLGKCHMONPACHBCHIJ $.
    $( [17-Aug-2004] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Distribute intersection over difference.  (Contributed by Scott Fenton,
       14-Apr-2011.) $)
    indifdir $p |- ( ( A \ B ) i^i C ) = ( ( A i^i C ) \ ( B i^i C ) ) $=
      ( vx cdif cin cv wcel wn wa wo pm3.24 intnan anass mtbir biorfi an32
      andi 3bitr4i ianor anbi2i bitr4i elin eldif anbi1i bitri notbii anbi12i
      eqriv ) DABEZCFZACFZBCFZEZDGZAHZUOBHZIZJZUOCHZJZUPUTJZUQUTJZIZJZUOUKHZUOU
      NHZVAVBURUTIZKZJZVEVBURJZVKVBVHJZKVAVJVLVKVLUPUTVHJZJVMUPUTLMUPUTVHNOPUPU
      RUTQVBURVHRSVDVIVBUQUTTUAUBVFUOUJHZUTJVAUOUJCUCVNUSUTUOABUDUEUFVGUOULHZUO
      UMHZIZJVEUOULUMUDVOVBVQVDUOACUCVPVCUOBCUCUGUHUFSUI $.
      $( [14-Apr-2011] $)
  $}

  $( DeMorgan's law for union.  Theorem 5.2(13) of [Stoll] p. 19. $)
  undm $p |- ( _V \ ( A u. B ) ) = ( ( _V \ A ) i^i ( _V \ B ) ) $=
    ( cvv difundi ) CABD $.
    $( [18-Aug-2004] $)

  $( DeMorgan's law for intersection.  Theorem 5.2(13') of [Stoll] p. 19. $)
  indm $p |- ( _V \ ( A i^i B ) ) = ( ( _V \ A ) u. ( _V \ B ) ) $=
    ( cvv difindi ) CABD $.
    $( [18-Aug-2004] $)

  $( A relationship involving double difference and union. $)
  difun1 $p |- ( A \ ( B u. C ) ) = ( ( A \ B ) \ C ) $=
    ( cvv cdif cin cun inass invdif undm ineq2i eqtr3i 3eqtr3i difeq1i ) ADBEZF
    ZCEZABCGZEZABEZCEPDCEZFAOUAFZFZQSAOUAHPCIADREZFUCSUDUBABCJKARILMPTCABINL $.
    $( [29-Aug-2004] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( An equality involving class union and class difference.  The first
       equality of Exercise 13 of [TakeutiZaring] p. 22.  (Contributed by Alan
       Sare, 17-Apr-2012.) $)
    undif3 $p |- ( A u. ( B \ C ) ) = ( ( A u. B ) \ ( C \ A ) ) $=
      ( vx cdif cun cv wcel wn wa wo elun eldif notbii pm4.53 bitri anbi12i
      orbi2i orc olc jca anim12i jaoi simpl orcd adantr adantl ccase impbii
      3bitri 3bitr4ri eqriv ) DABCEZFZABFZCAEZEZDGZUOHZURUPHZIZJURAHZURBHZKZURC
      HZIZVBKZJZURUQHURUNHZUSVDVAVGURABLVAVEVBIJZIVGUTVJURCAMNVEVBOPQURUOUPMVIV
      BURUMHZKVBVCVFJZKZVHURAUMLVKVLVBURBCMRVMVHVBVHVLVBVDVGVBVCSVBVFTUAVCVDVFV
      GVCVBTVFVBSUBUCVBVFVCVBVMVBVFJVBVLVBVFUDUEVLVBTVBVMVBVBVLSZUFVBVMVCVNUGUH
      UIUJUKUL $.
      $( [17-Apr-2012] $)

    $( Represent a set difference as an intersection with a larger difference.
       (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    difin2 $p |- ( A C_ C -> ( A \ B ) = ( ( C \ B ) i^i A ) ) $=
      ( vx wss cdif cin cv wcel wn wa wi wb ssel pm4.71 sylib anbi1d eldif
      elin anbi1i ancom anass bitr4i 3bitri 3bitr4g eqrdv ) ACEZDABFZCBFZAGZUGD
      HZAIZUKBIJZKULUKCIZKZUMKZUKUHIUKUJIZUGULUOUMUGULUNLULUOMACUKNULUNOPQUKABR
      UQUKUIIZULKUNUMKZULKZUPUKUIASURUSULUKCBRTUTULUSKUPUSULUAULUNUMUBUCUDUEUF
      $.
      $( [2-Sep-2009] $)
  $}

  $( Swap second and third argument of double difference. $)
  dif23 $p |- ( ( A \ B ) \ C ) = ( ( A \ C ) \ B ) $=
    ( cun cdif uncom difeq2i difun1 3eqtr3i ) ABCDZEACBDZEABECEACEBEJKABCFGABCH
    ACBHI $.
    $( [18-Aug-2004] $)

  $( Absorption-like law for class difference: you can remove a class only
     once.  (Contributed by FL, 2-Aug-2009.) $)
  difabs $p |- ( ( A \ B ) \ B ) = ( A \ B ) $=
    ( cun cdif difun1 unidm difeq2i eqtr3i ) ABBCZDABDZBDJABBEIBABFGH $.
    $( [2-Aug-2009] $)

  $( Two ways to express symmetric difference.  This theorem shows the
     equivalence of the definition of symmetric difference in [Stoll] p. 13 and
     the restated definition in Example 4.1 of [Stoll] p. 262. $)
  symdif1 $p |- ( ( A \ B ) u. ( B \ A ) ) = ( ( A u. B ) \ ( A i^i B ) ) $=
    ( cun cin cdif difundir difin incom difeq2i eqtri uneq12i eqtr2i ) ABCABDZE
    AMEZBMEZCABEZBAEZCABMFNPOQABGOBBADZEQMRBABHIBAGJKL $.
    $( [17-Aug-2004] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express symmetric difference.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    symdif2 $p |- ( ( A \ B ) u. ( B \ A ) ) =
                  { x | -. ( x e. A <-> x e. B ) } $=
      ( cv wcel wb wn cdif cun wo wa eldif orbi12i elun xor 3bitr4i abbi2i ) AD
      ZBEZRCEZFGZABCHZCBHZIZRUBEZRUCEZJSTGKZTSGKZJRUDEUAUEUGUFUHRBCLRCBLMRUBUCN
      STOPQ $.
      $( [26-Jun-2011] $) $( [17-Aug-2004] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Union of two class abstractions.  (The proof was shortened by Andrew
       Salmon, 26-Jun-2011.) $)
    unab $p |- ( { x | ph } u. { x | ps } ) = { x | ( ph \/ ps ) } $=
      ( vy cab wo cv wsbc wcel sbor df-clab orbi12i 3bitr4ri uneqri ) DACEZBCEZ
      ABFZCEZQCDGZHACSHZBCSHZFSRISOIZSPIZFABCDJQDCKUBTUCUAADCKBDCKLMN $.
      $( [26-Jun-2011] $) $( [29-Sep-2002] $)

    $( Intersection of two class abstractions.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    inab $p |- ( { x | ph } i^i { x | ps } ) = { x | ( ph /\ ps ) } $=
      ( vy cab wa cv wsbc wcel sban df-clab anbi12i 3bitr4ri ineqri ) DACEZBCEZ
      ABFZCEZQCDGZHACSHZBCSHZFSRISOIZSPIZFABCDJQDCKUBTUCUAADCKBDCKLMN $.
      $( [26-Jun-2011] $) $( [29-Sep-2002] $)

    $( Difference of two class abstractions.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    difab $p |- ( { x | ph } \ { x | ps } ) = { x | ( ph /\ -. ps ) } $=
      ( vy cab wn wa cv wsbc wcel sban df-clab notbii sbn bitr4i anbi12i
      3bitr4ri difeqri ) DACEZBCEZABFZGZCEZUBCDHZIACUDIZUACUDIZGUDUCJUDSJZUDTJZ
      FZGAUACDKUBDCLUGUEUIUFADCLUIBCUDIZFUFUHUJBDCLMBCDNOPQR $.
      $( [26-Jun-2011] $) $( [23-Oct-2004] $)
  $}

  $( Union of two restricted class abstractions. $)
  unrab $p |- ( { x e. A | ph } u. { x e. A | ps } ) =
              { x e. A | ( ph \/ ps ) } $=
    ( cv wcel wa cab cun wo crab unab andi abbii eqtr4i df-rab uneq12i 3eqtr4i
    ) CEDFZAGZCHZSBGZCHZIZSABJZGZCHZACDKZBCDKZIUECDKUDTUBJZCHUGTUBCLUFUJCSABMNO
    UHUAUIUCACDPBCDPQUECDPR $.
    $( [25-Mar-2004] $)

  $( Intersection of two restricted class abstractions. $)
  inrab $p |- ( { x e. A | ph } i^i { x e. A | ps } ) =
              { x e. A | ( ph /\ ps ) } $=
    ( cv wcel wa cab cin crab inab anandi abbii eqtr4i df-rab ineq12i 3eqtr4i
    ) CEDFZAGZCHZRBGZCHZIZRABGZGZCHZACDJZBCDJZIUDCDJUCSUAGZCHUFSUACKUEUICRABLMN
    UGTUHUBACDOBCDOPUDCDOQ $.
    $( [1-Sep-2006] $)

  ${
    $d x B $.
    $( Intersection with a restricted class abstraction. $)
    inrab2 $p |- ( { x e. A | ph } i^i B ) = { x e. ( A i^i B ) | ph } $=
      ( cv wcel wa cab cin crab inab elin anbi1i an32 bitri abbii eqtr4i
      df-rab abid2 eqcomi ineq12i 3eqtr4i ) BEZCFZAGZBHZUCDFZBHZIZUCCDIZFZAGZBH
      ZABCJZDIABUJJUIUEUGGZBHUMUEUGBKULUOBULUDUGGZAGUOUKUPAUCCDLMUDUGANOPQUNUFD
      UHABCRUHDBDSTUAABUJRUB $.
      $( [19-Nov-2007] $)
  $}

  $( Difference of two restricted class abstractions. $)
  difrab $p |- ( { x e. A | ph } \ { x e. A | ps } ) =
              { x e. A | ( ph /\ -. ps ) } $=
    ( cv wcel wa cab cdif wn crab difab anass simpr con3i anim2i wi pm3.2
    adantr con3d imdistani impbii bitr3i abbii eqtr4i df-rab difeq12i 3eqtr4i
    ) CEDFZAGZCHZUIBGZCHZIZUIABJZGZGZCHZACDKZBCDKZIUPCDKUNUJULJZGZCHURUJULCLUQV
    BCUQUJUOGZVBUIAUOMVCVBUOVAUJULBUIBNOPUJVAUOUJBULUIBULQAUIBRSTUAUBUCUDUEUSUK
    UTUMACDUFBCDUFUGUPCDUFUH $.
    $( [23-Oct-2004] $)

  ${
    $d x A $.
    $( Alternate definition of restricted class abstraction. $)
    dfrab2 $p |- { x e. A | ph } = ( { x | ph } i^i A ) $=
      ( crab cv wcel wa cab cin df-rab inab abid2 ineq1i eqtr3i incom 3eqtri )
      ABCDBECFZAGBHZCABHZIZSCIABCJQBHZSIRTQABKUACSBCLMNCSOP $.
      $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Transfer uniqueness to a smaller subclass. $)
    reuss2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\
                 ( E. x e. A ph /\ E! x e. B ps ) ) -> E! x e. A ph ) $=
      ( wrex wreu wa wss wi wral cv wcel wex weu df-rex df-reu anbi12i wmo wal
      df-ral ssel prth sylan exp4b com23 a2d imp4a alimdv imp sylan2b euimmo
      syl eu5 simplbi2 syl9 imp32 sylibr ) ACDFZBCEGZHDEIZABJZCDKZHZCLZDMZAHZCN
      ZVEEMZBHZCOZHZACDGZUSVHUTVKACDPBCEQRVDVLHVGCOZVMVDVHVKVNVDVKVGCSZVHVNVDVG
      VJJZCTZVKVOJVCVAVFVBJZCTZVQVBCDUAVAVSVQVAVRVPCVAVRVFAVJVAVFVBAVJJZVAVBVFV
      TVAVBVFAVJVAVFVIJVBVPDEVEUBVFVIABUCUDUEUFUGUHUIUJUKVGVJCULUMVNVHVOVGCUNUO
      UPUQACDQURUK $.
      $( [20-Oct-2005] $)

    $( Transfer uniqueness to a smaller subclass. $)
    reuss $p |- ( ( A C_ B /\ E. x e. A ph /\ E! x e. B ph ) ->
                E! x e. A ph ) $=
      ( wss wrex wreu wi wral wa cv wcel idd rgen reuss2 mpanl2 3impb ) CDEZABC
      FZABDGZABCGZRAAHZBCISTJUAUBBCBKCLAMNAABCDOPQ $.
      $( [2-Nov-2005] $) $( [21-Aug-1999] $)
$(
      ( wss wrex wreu cv wcel wa wex weu wmo wi wal ssel anim1d 19.21aiv
      euimmo syl eu5 biimpri ex syl9 imp32 df-reu sylibr df-rex anbi12i
  sylan2bOLD 3impb ) CDEZABCFZABDGZABCGZULBHZCIZAJZBKZUPDIZAJZBLZJZUOUMUNJULVC
      JURBLZUOULUSVBVDULVBURBMZUSVDULURVANZBOVBVENULVFBULUQUTACDUPPQRURVABSTUSV
      EVDVDUSVEJURBUAUBUCUDUEABCUFUGUMUSUNVBABCUHABDUFUIUJUK $.
$)

    $( Transfer uniqueness to a smaller class. $)
    reuun1 $p |- ( ( E. x e. A ph /\ E! x e. ( A u. B ) ( ph \/ ps ) )
                 -> E! x e. A ph ) $=
      ( cun wss wo wi wral wrex wreu wa ssun1 cv wcel orc a1i rgen reuss2
      mpanl12 ) DDEFZGAABHZIZCDJACDKUCCUBLMACDLDENUDCDUDCODPABQRSAUCCDUBTUA $.
      $( [21-Oct-2005] $)

    $( Transfer uniqueness to a smaller or larger class. $)
    reuun2 $p |- ( -. E. x e. B ph ->
             ( E! x e. ( A u. B ) ph <-> E! x e. A ph ) ) $=
      ( wrex wn cv wcel wa wo weu cun wreu wex wb df-rex notbii euor2 sylbi
      df-reu elun anbi1i andir orcom 3bitri eubii bitri 3bitr4g ) ABDEZFZBGZDHZ
      AIZUKCHZAIZJZBKZUOBKZABCDLZMZABCMUJUMBNZFUQUROUIVAABDPQUMUOBRSUTUKUSHZAIZ
      BKUQABUSTVCUPBVCUNULJZAIUOUMJUPVBVDAUKCDUAUBUNULAUCUOUMUDUEUFUGABCTUH $.
      $( [21-Oct-2005] $)

    $( Restricted uniqueness "picks" a member of a subclass. $)
    reupick $p |- ( ( ( A C_ B /\ ( E. x e. A ph /\ E! x e. B ph ) ) /\ ph ) ->
                  ( x e. A <-> x e. B ) ) $=
      ( wss wrex wreu wa cv wcel wi ssel ad2antrr wex weu df-rex df-reu
      anbi12i ancrd anim1d an32 syl6ib eximdv eupick ex syl9 com23 imp32
      sylan2b exp3a imp impbid ) CDEZABCFZABDGZHZHZAHBIZCJZURDJZUMUSUTKUPACDURL
      ZMUQAUTUSKUQUTAUSUQUTAUSUPUMUSAHZBNZUTAHZBOZHVDUSKZUNVCUOVEABCPABDQRUMVCV
      EVFUMVEVCVFUMVCVDUSHZBNZVEVFUMVBVGBUMVBUTUSHZAHVGUMUSVIAUMUSUTVASTUTUSAUA
      UBUCVEVHVFVDUSBUDUEUFUGUHUIUJUGUKUL $.
      $( [21-Aug-1999] $)
  $}

