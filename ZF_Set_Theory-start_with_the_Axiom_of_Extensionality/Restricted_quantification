$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Restricted quantification
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend wff notation to include restricted universal quantification. $)
  wral $a wff A. x e. A ph $.

  $( Extend wff notation to include restricted existential quantification. $)
  wrex $a wff E. x e. A ph $.

  $( Extend wff notation to include restricted existential uniqueness. $)
  wreu $a wff E! x e. A ph $.

  $( Extend class notation to include the restricted class abstraction (class
     builder). $)
  crab $a class { x e. A | ph } $.

  $( Define restricted universal quantification.  Special case of Definition
     4.15(3) of [TakeutiZaring] p. 22. $)
  df-ral $a |- ( A. x e. A ph <-> A. x ( x e. A -> ph ) ) $.

  $( Define restricted existential quantification.  Special case of Definition
     4.15(4) of [TakeutiZaring] p. 22. $)
  df-rex $a |- ( E. x e. A ph <-> E. x ( x e. A /\ ph ) ) $.

  $( Define restricted existential uniqueness. $)
  df-reu $a |- ( E! x e. A ph <-> E! x ( x e. A /\ ph ) ) $.

  $( Define a restricted class abstraction (class builder), which is the class
     of all ` x ` in ` A ` such that ` ph ` is true.  Definition of
     [TakeutiZaring] p. 20. $)
  df-rab $a |- { x e. A | ph } = { x | ( x e. A /\ ph ) } $.

  $( Relationship between restricted universal and existential quantifiers. $)
  ralnex $p |- ( A. x e. A -. ph <-> -. E. x e. A ph ) $=
    ( cv wcel wn wi wal wa wex wral wrex alinexa df-ral df-rex notbii 3bitr4i
    ) BDCEZAFZGBHRAIBJZFSBCKABCLZFRABMSBCNUATABCOPQ $.
    $( [21-Jan-1997] $)

  $( Relationship between restricted universal and existential quantifiers. $)
  rexnal $p |- ( E. x e. A -. ph <-> -. A. x e. A ph ) $=
    ( cv wcel wn wa wex wi wal wrex wral exanali df-rex df-ral notbii 3bitr4i
    ) BDCEZAFZGBHRAIBJZFSBCKABCLZFRABMSBCNUATABCOPQ $.
    $( [21-Jan-1997] $)

  $( Relationship between restricted universal and existential quantifiers. $)
  dfral2 $p |- ( A. x e. A ph <-> -. E. x e. A -. ph ) $=
    ( wn wrex wral rexnal con2bii ) ADBCEABCFABCGH $.
    $( [21-Jan-1997] $)

  $( Relationship between restricted universal and existential quantifiers. $)
  dfrex2 $p |- ( E. x e. A ph <-> -. A. x e. A -. ph ) $=
    ( wn wral wrex ralnex con2bii ) ADBCEABCFABCGH $.
    $( [21-Jan-1997] $)

  ${
    ralbida.1 $e |- ( ph -> A. x ph ) $.
    ralbida.2 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    ralbida $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral pm5.74da albid df-ral 3bitr4g ) ADHEIZBJZDKQCJZDKBD
      ELCDELARSDFAQBCGMNBDEOCDEOP $.
      $( [6-Oct-2003] $)

    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    rexbida $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( cv wcel wa wex wrex pm5.32da exbid df-rex 3bitr4g ) ADHEIZBJZDKQCJZDKBD
      ELCDELARSDFAQBCGMNBDEOCDEOP $.
      $( [6-Oct-2003] $)
  $}

  ${
    $d x ph $.
    ralbidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    ralbidva $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( ax-17 ralbida ) ABCDEADGFH $.
      $( [4-Mar-1997] $)

    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    rexbidva $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( ax-17 rexbida ) ABCDEADGFH $.
      $( [9-Mar-1997] $)
  $}

  ${
    ralbid.1 $e |- ( ph -> A. x ph ) $.
    ralbid.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    ralbid $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( wb cv wcel adantr ralbida ) ABCDEFABCHDIEJGKL $.
      $( [27-Jun-1998] $)

    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    rexbid $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( wb cv wcel adantr rexbida ) ABCDEFABCHDIEJGKL $.
      $( [27-Jun-1998] $)
  $}

  ${
    $d x ph $.
    ralbidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    ralbidv $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( ax-17 ralbid ) ABCDEADGFH $.
      $( [20-Nov-1994] $)

    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    rexbidv $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( ax-17 rexbid ) ABCDEADGFH $.
      $( [20-Nov-1994] $)
  $}

  ${
    $d x ph $.
    ralbidv2.1 $e |- ( ph -> ( ( x e. A -> ps ) <-> ( x e. B -> ch ) ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    ralbidv2 $p |- ( ph -> ( A. x e. A ps <-> A. x e. B ch ) ) $=
      ( cv wcel wi wal wral albidv df-ral 3bitr4g ) ADHZEIBJZDKPFICJZDKBDELCDFL
      AQRDGMBDENCDFNO $.
      $( [6-Apr-1997] $)
  $}

  ${
    $d x ph $.
    rexbidv2.1 $e |- ( ph -> ( ( x e. A /\ ps ) <-> ( x e. B /\ ch ) ) ) $.
    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    rexbidv2 $p |- ( ph -> ( E. x e. A ps <-> E. x e. B ch ) ) $=
      ( cv wcel wa wex wrex exbidv df-rex 3bitr4g ) ADHZEIBJZDKPFICJZDKBDELCDFL
      AQRDGMBDENCDFNO $.
      $( [22-May-1999] $)
  $}

  ${
    ralbii.1 $e |- ( ph <-> ps ) $.
    $( Inference adding restricted universal quantifier to both sides of an
       equivalence. $)
    ralbii $p |- ( A. x e. A ph <-> A. x e. A ps ) $=
      ( wb wral biid hbth a1i ralbid ax-mp ) AAFZACDGBCDGFAHZMABCDMCNIABFMEJKL
      $.
      $( [23-Nov-1994] $)

    $( Inference adding restricted existential quantifier to both sides of an
       equivalence. $)
    rexbii $p |- ( E. x e. A ph <-> E. x e. A ps ) $=
      ( wb wrex biid hbth a1i rexbid ax-mp ) AAFZACDGBCDGFAHZMABCDMCNIABFMEJKL
      $.
      $( [23-Nov-1994] $)

    $( Inference adding 2 restricted universal quantifiers to both sides of an
       equivalence. $)
    2ralbii $p |- ( A. x e. A A. y e. B ph <-> A. x e. A A. y e. B ps ) $=
      ( wral ralbii ) ADFHBDFHCEABDFGII $.
      $( [1-Aug-2004] $)

    $( Inference adding 2 restricted existential quantifiers to both sides of
       an equivalence. $)
    2rexbii $p |- ( E. x e. A E. y e. B ph <-> E. x e. A E. y e. B ps ) $=
      ( wrex rexbii ) ADFHBDFHCEABDFGII $.
      $( [11-Nov-1995] $)
  $}

  ${
    ralbii2.1 $e |- ( ( x e. A -> ph ) <-> ( x e. B -> ps ) ) $.
    $( Inference adding different restricted universal quantifiers to each side
       of an equivalence. $)
    ralbii2 $p |- ( A. x e. A ph <-> A. x e. B ps ) $=
      ( cv wcel wi wal wral albii df-ral 3bitr4i ) CGZDHAIZCJOEHBIZCJACDKBCEKPQ
      CFLACDMBCEMN $.
      $( [15-Aug-2005] $)
  $}

  ${
    rexbii2.1 $e |- ( ( x e. A /\ ph ) <-> ( x e. B /\ ps ) ) $.
    $( Inference adding different restricted existential quantifiers to each
       side of an equivalence. $)
    rexbii2 $p |- ( E. x e. A ph <-> E. x e. B ps ) $=
      ( cv wcel wa wex wrex exbii df-rex 3bitr4i ) CGZDHAIZCJOEHBIZCJACDKBCEKPQ
      CFLACDMBCEMN $.
      $( [4-Feb-2004] $)
  $}

  ${
    ralbiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Inference adding restricted universal quantifier to both sides of an
       equivalence. $)
    ralbiia $p |- ( A. x e. A ph <-> A. x e. A ps ) $=
      ( cv wcel pm5.74i ralbii2 ) ABCDDCFDGABEHI $.
      $( [26-Nov-2000] $)

    $( Inference adding restricted existential quantifier to both sides of an
       equivalence. $)
    rexbiia $p |- ( E. x e. A ph <-> E. x e. A ps ) $=
      ( cv wcel pm5.32i rexbii2 ) ABCDDCFDGABEHI $.
      $( [26-Oct-1999] $)
  $}

  ${
    $d x y $.  $d y A $.
    2rexbiia.1 $e |- ( ( x e. A /\ y e. B ) -> ( ph <-> ps ) ) $.
    $( Inference adding 2 restricted existential quantifiers to both sides of
       an equivalence. $)
    2rexbiia $p |- ( E. x e. A E. y e. B ph <-> E. x e. A E. y e. B ps ) $=
      ( wrex cv wcel rexbidva rexbiia ) ADFHBDFHCECIEJABDFGKL $.
      $( [1-Aug-2004] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( Double restricted universal quantification. $)
    r2al $p |- ( A. x e. A A. y e. B ph <->
               A. x A. y ( ( x e. A /\ y e. B ) -> ph ) ) $=
      ( wral cv wcel wi wal wa df-ral 19.21v impexp albii imbi2i 3bitr4i
      bitr4i ) ACEFZBDFBGDHZSIZBJTCGEHZKAIZCJZBJSBDLUDUABTUBAIZIZCJTUECJZIUDUAT
      UECMUCUFCTUBANOSUGTACELPQOR $.
      $( [19-Nov-1995] $)
  $}

  ${
    $d x y $.  $d y A $.
    2ralbida.1 $e |- ( ph -> A. x ph ) $.
    2ralbida.2 $e |- ( ph -> A. y ph ) $.
    2ralbida.3 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifier (deduction
       rule). $)
    2ralbida $p |- ( ph ->
                     ( A. x e. A A. y e. B ps <-> A. x e. A A. y e. B ch ) ) $=
      ( wral cv wcel wa ax-17 hban wb anassrs ralbida ) ABEGKCEGKDFHADLFMZNBCEG
      ATEITEOPATELGMBCQJRSS $.
      $( [24-Feb-2004] $)
  $}

  ${
    $d x y ph $.  $d y A $.
    2ralbidva.1 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifiers (deduction
       rule). $)
    2ralbidva $p |- ( ph ->
                     ( A. x e. A A. y e. B ps <-> A. x e. A A. y e. B ch ) ) $=
      ( ax-17 2ralbida ) ABCDEFGADIAEIHJ $.
      $( [4-Mar-1997] $)

    $( Formula-building rule for restricted existential quantifiers (deduction
       rule). $)
    2rexbidva $p |- ( ph ->
                    ( E. x e. A E. y e. B ps <-> E. x e. A E. y e. B ch ) ) $=
      ( wrex cv wcel wa wb anassrs rexbidva ) ABEGICEGIDFADJFKZLBCEGAPEJGKBCMHN
      OO $.
      $( [15-Dec-2004] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    2ralbidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted universal quantifiers (deduction
       rule).  (Unnecessary distinct variable restrictions were removed by
       Szymon Jaroszewicz, 16-Mar-2006.) $)
    2ralbidv $p |- ( ph ->
                     ( A. x e. A A. y e. B ps <-> A. x e. A A. y e. B ch ) ) $=
      ( wral ralbidv ) ABEGICEGIDFABCEGHJJ $.
      $( [16-Mar-2007] $) $( [28-Jan-2006] $)

    $( Formula-building rule for restricted existential quantifiers (deduction
       rule). $)
    2rexbidv $p |- ( ph ->
                    ( E. x e. A E. y e. B ps <-> E. x e. A E. y e. B ch ) ) $=
      ( wrex rexbidv ) ABEGICEGIDFABCEGHJJ $.
      $( [28-Jan-2006] $)

    $( Formula-building rule for restricted quantifiers (deduction rule). $)
    rexralbidv $p |- ( ph ->
                    ( E. x e. A A. y e. B ps <-> E. x e. A A. y e. B ch ) ) $=
      ( wral ralbidv rexbidv ) ABEGICEGIDFABCEGHJK $.
      $( [28-Jan-2006] $)
  $}

  $( A transformation of restricted quantifiers and logical connectives. $)
  ralinexa $p |- ( A. x e. A ( ph -> -. ps ) <-> -. E. x e. A ( ph /\ ps ) ) $=
    ( wn wi wral wa wrex imnan ralbii ralnex bitri ) ABEFZCDGABHZEZCDGOCDIENPCD
    ABJKOCDLM $.
    $( [4-Sep-2005] $)

  $( A transformation of restricted quantifiers and logical connectives. $)
  rexanali $p |- ( E. x e. A ( ph /\ -. ps ) <-> -. A. x e. A ( ph -> ps ) ) $=
    ( wn wa wrex wi wral annim rexbii rexnal bitri ) ABEFZCDGABHZEZCDGOCDIENPCD
    ABJKOCDLM $.
    $( [4-Sep-2005] $)

  ${
    $d x A $.  $d x B $.
    $( Two ways to say " ` A ` belongs to ` B ` ." $)
    risset $p |- ( A e. B <-> E. x e. B x = A ) $=
      ( cv wcel wceq wa wex wrex exancom df-rex df-clel 3bitr4ri ) ADZCEZNBFZGA
      HPOGAHPACIBCEOPAJPACKABCLM $.
      $( [22-Nov-1994] $)
  $}

  ${
    hbral.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbral.2 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for restricted quantification.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 13-Dec-2009.) $)
    hbral $p |- ( A. y e. A ph -> A. x A. y e. A ph ) $=
      ( cv wcel wi wal wral hbim hbal df-ral albii 3imtr4i ) CGDHZAIZCJZSBJACDK
      ZTBJRBCQABEFLMACDNZTSBUAOP $.
      $( [13-Dec-2009] $) $( [1-Sep-1999] $)
  $}

  $( ` x ` is not free in ` A. x e. A ph ` . $)
  hbra1 $p |- ( A. x e. A ph -> A. x A. x e. A ph ) $=
    ( cv wcel wi wal wral hba1 df-ral albii 3imtr4i ) BDCEAFZBGZNBGABCHZOBGMBIA
    BCJZONBPKL $.
    $( [18-Oct-1996] $)

  ${
    $d A y $.  $d x y $.
    $( Similar to Lemma 24 of [Monk2] p. 114, except the quantification of the
       antecedent is restricted.  Derived automatically from ~ hbra2VD .
       Contributed by Alan Sare 31-Dec-2011. $)
    hbra2 $p |- ( A. x e. A A. y e. B ph -> A. y A. x e. A A. y e. B ph ) $=
      ( cv wcel wi wal wral ax-17 hba1 hbral df-ral ralbii albii 3imtr4i ) CFEG
      AHZCIZBDJZTCIACEJZBDJZUBCISCBDBFDGCKRCLMUASBDACENOZUBTCUCPQ $.
      $( [31-Dec-2011] $)
  $}

  ${
    hbrex.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbrex.2 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for restricted quantification.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 13-Dec-2009.) $)
    hbrex $p |- ( E. y e. A ph -> A. x E. y e. A ph ) $=
      ( cv wcel wa wex wal wrex hban hbex df-rex albii 3imtr4i ) CGDHZAIZCJZTBK
      ACDLZUABKSBCRABEFMNACDOZUATBUBPQ $.
      $( [13-Dec-2009] $) $( [1-Sep-1999] $)
  $}

  $( ` x ` is not free in ` E. x e. A ph ` . $)
  hbre1 $p |- ( E. x e. A ph -> A. x E. x e. A ph ) $=
    ( cv wcel wa wex wal wrex hbe1 df-rex albii 3imtr4i ) BDCEAFZBGZOBHABCIZPBH
    NBJABCKZPOBQLM $.
    $( [19-Mar-1997] $)

  ${
    $d x y z $.  $d y z A $.  $d z B $.
    $( Triple restricted universal quantification. $)
    r3al $p |- ( A. x e. A A. y e. B A. z e. C ph <->
               A. x A. y A. z ( ( x e. A /\ y e. B /\ z e. C ) -> ph ) ) $=
      ( cv wcel wa wi wal wral w3a df-ral r2al ralbii 3anass imbi1i impexp
      bitri albii 19.21v 3bitr4i ) CHFIZDHGIZJZAKZDLZCLZBEMBHEIZUJKZBLADGMCFMZB
      EMUKUEUFNZAKZDLZCLZBLUJBEOUMUJBEACDFGPQUQULBUQUKUIKZCLULUPURCUPUKUHKZDLUR
      UOUSDUOUKUGJZAKUSUNUTAUKUEUFRSUKUGATUAUBUKUHDUCUAUBUKUICUCUAUBUD $.
      $( [19-Nov-1995] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( Double restricted existential quantification. $)
    r2ex $p |- ( E. x e. A E. y e. B ph <->
               E. x E. y ( ( x e. A /\ y e. B ) /\ ph ) ) $=
      ( wrex cv wcel wa wex df-rex 19.42v anass exbii anbi2i 3bitr4i bitr4i )
      ACEFZBDFBGDHZRIZBJSCGEHZIAIZCJZBJRBDKUCTBSUAAIZIZCJSUDCJZIUCTSUDCLUBUECSU
      AAMNRUFSACEKOPNQ $.
      $( [11-Nov-1995] $)
  $}

  $( Universal quantification implies restricted quantification. $)
  alral $p |- ( A. x ph -> A. x e. A ph ) $=
    ( wal cv wcel wi wral ax-1 alimi df-ral sylibr ) ABDBECFZAGZBDABCHANBAMIJAB
    CKL $.
    $( [20-Oct-2006] $)

  $( Restricted existence implies existence. $)
  rexex $p |- ( E. x e. A ph -> E. x ph ) $=
    ( wrex cv wcel wa wex df-rex pm3.27 eximi sylbi ) ABCDBECFZAGZBHABHABCINABM
    AJKL $.
    $( [11-Nov-1995] $)

  $( Restricted specialization. $)
  ra4 $p |- ( A. x e. A ph -> ( x e. A -> ph ) ) $=
    ( wral cv wcel wi wal df-ral ax-4 sylbi ) ABCDBECFAGZBHLABCILBJK $.
    $( [17-Oct-1996] $)

  $( Restricted specialization. $)
  ra4e $p |- ( ( x e. A /\ ph ) -> E. x e. A ph ) $=
    ( cv wcel wa wex wrex 19.8a df-rex sylibr ) BDCEAFZLBGABCHLBIABCJK $.
    $( [12-Oct-1999] $)

  $( Restricted specialization. $)
  ra42 $p |- ( A. x e. A A. y e. B ph -> ( ( x e. A /\ y e. B ) -> ph ) ) $=
    ( wral cv wcel wi ra4 syl6 imp3a ) ACEFZBDFZBGDHZCGEHZANOMPAIMBDJACEJKL $.
    $( [11-Feb-1997] $)

  ${
    rspec.1 $e |- A. x e. A ph $.
    $( Specialization rule for restricted quantification. $)
    rspec $p |- ( x e. A -> ph ) $=
      ( wral cv wcel wi ra4 ax-mp ) ABCEBFCGAHDABCIJ $.
      $( [19-Nov-1994] $)
  $}

  ${
    rgen.1 $e |- ( x e. A -> ph ) $.
    $( Generalization rule for restricted quantification. $)
    rgen $p |- A. x e. A ph $=
      ( wral cv wcel wi df-ral mpgbir ) ABCEBFCGAHBABCIDJ $.
      $( [19-Nov-1994] $)
  $}

  ${
    $d y z A $.  $d x z $.
    rgen2a.1 $e |- ( ( x e. A /\ y e. A ) -> ph ) $.
    $( Generalization rule for restricted quantification.  Note that ` x ` and
       ` y ` needn't be distinct.  (The proof was shortened by Andrew Salmon,
       25-May-2011.) $)
    rgen2a $p |- A. x e. A A. y e. A ph $=
      ( vz wral cv wcel wi wal wceq ex eleq1 imbi1d mpbiri pm2.43d alimi a1d
      wn ax-17 dvelim syl6 pm2.61i df-ral sylibr rgen ) ACDGZBDBHZDIZCHZDIZAJZC
      KZUHUKUILZCKZUJUNJUPUNUJUOUMCUOULAUOULUMJUJUMJUJULAEMZUOULUJUMUKUIDNOPQRS
      UPTUJUJCKUNFHZDIZUJCBFUSCUAURUIDNUBUJUMCUQRUCUDACDUEUFUG $.
      $( [25-May-2011] $) $( [23-Nov-1994] $)

    $( Generalization rule for restricted quantification.  Note that ` x ` and
       ` y ` needn't be distinct. $)
    rgen2aOLD $p |- A. x e. A A. y e. A ph $=
      ( vz wral cv wcel wi wal wceq ex ax-gen wb eleq1 a4s imbi1d dral2 mpbiri
      pm2.43 alimi ax-1 3syl wn ax-17 dvelim syl6 pm2.61i df-ral sylibr rgen )
      ACDGZBDBHZDIZCHZDIZAJZCKZUMUPUNLZCKZUOUSJZVAUQURJZCKZUSVBVAVDUOURJZCKVECU
      OUQAEMZNVCVECBCVAUQUOURUTUQUOOCUPUNDPQRSTVCURCUQAUAUBUSUOUCUDVAUEUOUOCKUS
      FHZDIZUOCBFVHCUFVGUNDPUGUOURCVFUBUHUIACDUJUKUL $.
      $( [23-Nov-1994] $)
  $}

  ${
    mprg.1 $e |- ( A. x e. A ph -> ps ) $.
    mprg.2 $e |- ( x e. A -> ph ) $.
    $( Modus ponens combined with restricted generalization. $)
    mprg $p |- ps $=
      ( wral rgen ax-mp ) ACDGBACDFHEI $.
      $( [10-Aug-2004] $)
  $}

  ${
    mprgbir.1 $e |- ( ph <-> A. x e. A ps ) $.
    mprgbir.2 $e |- ( x e. A -> ps ) $.
    $( Modus ponens on biconditional combined with restricted
       generalization. $)
    mprgbir $p |- ph $=
      ( wral rgen mpbir ) ABCDGBCDFHEI $.
      $( [21-Mar-2004] $)
  $}

  $( Distribution of restricted quantification over implication. $)
  ralim $p |- ( A. x e. A ( ph -> ps ) ->
               ( A. x e. A ph -> A. x e. A ps ) ) $=
    ( wi wral cv wcel wal df-ral ax-2 al2imi sylbi 3imtr4g ) ABEZCDFZCGDHZAEZCI
    ZQBEZCIZACDFBCDFPQOEZCISUAEOCDJUBRTCQABKLMACDJBCDJN $.
    $( [9-Feb-1997] $)

  ${
    ralimi2.1 $e |- ( ( x e. A -> ph ) -> ( x e. B -> ps ) ) $.
    $( Inference quantifying both antecedent and consequent. $)
    ralimi2 $p |- ( A. x e. A ph -> A. x e. B ps ) $=
      ( cv wcel wi wal wral alimi df-ral 3imtr4i ) CGZDHAIZCJOEHBIZCJACDKBCEKPQ
      CFLACDMBCEMN $.
      $( [22-Feb-2004] $)
  $}

  ${
    ralimia.1 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Inference quantifying both antecedent and consequent. $)
    ralimia $p |- ( A. x e. A ph -> A. x e. A ps ) $=
      ( cv wcel a2i ralimi2 ) ABCDDCFDGABEHI $.
      $( [19-Jul-1996] $)
  $}

  ${
    ralimiaa.1 $e |- ( ( x e. A /\ ph ) -> ps ) $.
    $( Inference quantifying both antecedent and consequent. $)
    ralimiaa $p |- ( A. x e. A ph -> A. x e. A ps ) $=
      ( cv wcel ex ralimia ) ABCDCFDGABEHI $.
      $( [4-Aug-2007] $)
  $}

  ${
    ralimi.1 $e |- ( ph -> ps ) $.
    $( Inference quantifying both antecedent and consequent, with strong
       hypothesis. $)
    ralimi $p |- ( A. x e. A ph -> A. x e. A ps ) $=
      ( wi cv wcel a1i ralimia ) ABCDABFCGDHEIJ $.
      $( [4-Mar-1997] $)
  $}

  ${
    ral2imi.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference quantifying antecedent, nested antecedent, and consequent,
       with a strong hypothesis. $)
    ral2imi $p |- ( A. x e. A ph -> ( A. x e. A ps -> A. x e. A ch ) ) $=
      ( wral wi ralimi ralim syl ) ADEGBCHZDEGBDEGCDEGHALDEFIBCDEJK $.
      $( [19-Dec-2006] $)
  $}

  ${
    ralimdaa.1 $e |- ( ph -> A. x ph ) $.
    ralimdaa.2 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.20 of [Margaris] p. 90. $)
    ralimdaa $p |- ( ph -> ( A. x e. A ps -> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral ex a2d alimd df-ral 3imtr4g ) ADHEIZBJZDKRCJZDKBDEL
      CDELASTDFARBCARBCJGMNOBDEPCDEPQ $.
      $( [22-Sep-2003] $)
  $}

  ${
    $d x ph $.
    ralimdvaa.1 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.20 of [Margaris] p. 90. $)
    ralimdvaa $p |- ( ph -> ( A. x e. A ps -> A. x e. A ch ) ) $=
      ( ax-17 ralimdaa ) ABCDEADGFH $.
      $( [22-May-1999] $)
  $}

  ${
    $d x ph $.
    ralimdv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.20 of [Margaris] p. 90. $)
    ralimdv $p |- ( ph -> ( A. x e. A ps -> A. x e. A ch ) ) $=
      ( wi cv wcel adantr ralimdvaa ) ABCDEABCGDHEIFJK $.
      $( [8-Oct-2003] $)
  $}

  ${
    $d x ph $.
    ralimdv2.1 $e |- ( ph -> ( ( x e. A -> ps ) -> ( x e. B -> ch ) ) ) $.
    $( Inference quantifying both antecedent and consequent. $)
    ralimdv2 $p |- ( ph -> ( A. x e. A ps -> A. x e. B ch ) ) $=
      ( cv wcel wi wal wral alimdv df-ral 3imtr4g ) ADHZEIBJZDKPFICJZDKBDELCDFL
      AQRDGMBDENCDFNO $.
      $( [1-Feb-2005] $)
  $}

  ${
    r19.21ai.1 $e |- ( ph -> A. x ph ) $.
    r19.21ai.2 $e |- ( ph -> ( x e. A -> ps ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21ai $p |- ( ph -> A. x e. A ps ) $=
      ( cv wcel wi wal wral 19.21ai df-ral sylibr ) ACGDHBIZCJBCDKAOCEFLBCDMN
      $.
      $( [10-Oct-1999] $)
  $}

  ${
    $d x ph $.
    r19.21aiv.1 $e |- ( ph -> ( x e. A -> ps ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21aiv $p |- ( ph -> A. x e. A ps ) $=
      ( ax-17 r19.21ai ) ABCDACFEG $.
      $( [22-Nov-1994] $)
  $}

  ${
    $d x ph $.
    r19.21aiva.1 $e |- ( ( ph /\ x e. A ) -> ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21aiva $p |- ( ph -> A. x e. A ps ) $=
      ( cv wcel ex r19.21aiv ) ABCDACFDGBEHI $.
      $( [2-Jan-2006] $)
  $}

  ${
    $( Theorem 19.21 of [Margaris] p. 90 with restricted quantifiers (closed
       theorem version). $)
    r19.21t $p |- ( A. x ( ph -> A. x ph ) ->
             ( A. x e. A ( ph -> ps ) <-> ( ph -> A. x e. A ps ) ) ) $=
      ( wal wi cv wcel wral 19.21t bi2.04 albii syl5bb df-ral imbi2i 3bitr4g )
      AACEFCEZCGDHZABFZFZCEZARBFZCEZFZSCDIABCDIZFQAUBFZCEUDUAAUBCJTUFCRABKLMSCD
      NUEUCABCDNOP $.
      $( [1-Mar-2008] $)
  $}

  ${
    $d x ph $.
    $( Theorem 19.21 of [Margaris] p. 90 with restricted quantifiers.  (The
       proof was shortened by Andrew Salmon, 30-May-2011.) $)
    r19.21v $p |- ( A. x e. A ( ph -> ps ) <-> ( ph -> A. x e. A ps ) ) $=
      ( wal wi wral wb r19.21t ax-17 mpg ) AACEFABFCDGABCDGFHCABCDIACJK $.
      $( [30-May-2011] $) $( [15-Oct-2003] $)

    $( Theorem 19.21 of [Margaris] p. 90 with restricted quantifiers. $)
    r19.21vOLD $p |- ( A. x e. A ( ph -> ps ) <-> ( ph -> A. x e. A ps ) ) $=
      ( wal wi wral wb ax-17 ax-gen r19.21t ax-mp ) AACEFZCEABFCDGABCDGFHMCACIJ
      ABCDKL $.
      $( [15-Oct-2003] $)
  $}

  ${
    r19.21ad.1 $e |- ( ph -> A. x ph ) $.
    r19.21ad.2 $e |- ( ps -> A. x ps ) $.
    r19.21ad.3 $e |- ( ph -> ( ps -> ( x e. A -> ch ) ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21ad $p |- ( ph -> ( ps -> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral 19.21ad df-ral syl6ibr ) ABDIEJCKZDLCDEMABQDFGHNCDE
      OP $.
      $( [16-Feb-2004] $)
  $}

  ${
    $d x ph $.  $d x ps $.
    r19.21adv.1 $e |- ( ph -> ( ps -> ( x e. A -> ch ) ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21adv $p |- ( ph -> ( ps -> A. x e. A ch ) ) $=
      ( ax-17 r19.21ad ) ABCDEADGBDGFH $.
      $( [27-May-1998] $)
  $}

  ${
    $d x ph $.  $d x ps $.
    r19.21adva.1 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21adva $p |- ( ph -> ( ps -> A. x e. A ch ) ) $=
      ( cv wcel wi ex com23 r19.21adv ) ABCDEADGEHZBCAMBCIFJKL $.
      $( [2-Feb-2008] $)
  $}

  ${
    $d x y ph $.  $d y A $.
    r19.21aivv.1 $e |- ( ph -> ( ( x e. A /\ y e. B ) -> ps ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version with double quantification.) $)
    r19.21aivv $p |- ( ph -> A. x e. A A. y e. B ps ) $=
      ( wral cv wcel exp3a r19.21adv r19.21aiv ) ABDFHCEACIEJZBDFANDIFJBGKLM $.
      $( [24-Jul-2004] $)
  $}

  ${
    $d x y ph $.  $d x y ps $.  $d y A $.
    r19.21advv.1 $e |- ( ph -> ( ps -> ( ( x e. A /\ y e. B ) -> ch ) ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version with double quantification.) $)
    r19.21advv $p |- ( ph -> ( ps -> A. x e. A A. y e. B ch ) ) $=
      ( wral wa cv wcel wi imp r19.21aivv ex ) ABCEGIDFIABJCDEFGABDKFLEKGLJCMHN
      OP $.
      $( [1-Jun-2005] $)
  $}

  ${
    $d x y ph $.  $d x y ps $.  $d y A $.
    r19.21advva.1 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> ( ps -> ch ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version with double quantification.) $)
    r19.21advva $p |- ( ph -> ( ps -> A. x e. A A. y e. B ch ) ) $=
      ( cv wcel wa wi ex com23 r19.21advv ) ABCDEFGADIFJEIGJKZBCAPBCLHMNO $.
      $( [2-Feb-2008] $)
  $}

  ${
    $d x y $.  $d y A $.
    rgen2.1 $e |- ( ( x e. A /\ y e. B ) -> ph ) $.
    $( Generalization rule for restricted quantification. $)
    rgen2 $p |- A. x e. A A. y e. B ph $=
      ( wral cv wcel r19.21aiva rgen ) ACEGBDBHDIACEFJK $.
      $( [30-May-1999] $)
  $}

  ${
    $d y z A $.  $d z B $.  $d x y z $.
    rgen3.1 $e |- ( ( x e. A /\ y e. B /\ z e. C ) -> ph ) $.
    $( Generalization rule for restricted quantification. $)
    rgen3 $p |- A. x e. A A. y e. B A. z e. C ph $=
      ( wral cv wcel wa 3expa r19.21aiva rgen2 ) ADGIBCEFBJEKZCJFKZLADGPQDJGKAH
      MNO $.
      $( [12-Jan-2008] $)
  $}

  ${
    r19.21bi.1 $e |- ( ph -> A. x e. A ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21bi $p |- ( ( ph /\ x e. A ) -> ps ) $=
      ( cv wcel wi wral wal df-ral sylib 19.21bi imp ) ACFDGZBAOBHZCABCDIPCJEBC
      DKLMN $.
      $( [20-Nov-1994] $)
  $}

  ${
    rspec2.1 $e |- A. x e. A A. y e. B ph $.
    $( Specialization rule for restricted quantification. $)
    rspec2 $p |- ( ( x e. A /\ y e. B ) -> ph ) $=
      ( cv wcel wral rspec r19.21bi ) BGDHACEACEIBDFJK $.
      $( [20-Nov-1994] $)
  $}

  ${
    rspec3.1 $e |- A. x e. A A. y e. B A. z e. C ph $.
    $( Specialization rule for restricted quantification. $)
    rspec3 $p |- ( ( x e. A /\ y e. B /\ z e. C ) -> ph ) $=
      ( cv wcel wa wral rspec2 r19.21bi 3impa ) BIEJZCIFJZDIGJAPQKADGADGLBCEFHM
      NO $.
      $( [20-Nov-1994] $)
  $}

  ${
    r19.21be.1 $e |- ( ph -> A. x e. A ps ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.21be $p |- A. x e. A ( ph -> ps ) $=
      ( wi cv wcel r19.21bi expcom rgen ) ABFCDACGDHBABCDEIJK $.
      $( [21-Nov-1994] $)
  $}

  ${
    nrex.1 $e |- ( x e. A -> -. ps ) $.
    $( Inference adding restricted existential quantifier to negated wff. $)
    nrex $p |- -. E. x e. A ps $=
      ( wn wral wrex rgen ralnex mpbi ) AEZBCFABCGEKBCDHABCIJ $.
      $( [16-Oct-2003] $)
  $}

  ${
    $d x ph $.
    nrexdv.1 $e |- ( ( ph /\ x e. A ) -> -. ps ) $.
    $( Deduction adding restricted existential quantifier to negated wff. $)
    nrexdv $p |- ( ph -> -. E. x e. A ps ) $=
      ( wn wral wrex r19.21aiva ralnex sylib ) ABFZCDGBCDHFALCDEIBCDJK $.
      $( [16-Oct-2003] $)
  $}

  $( Theorem 19.22 of [Margaris] p. 90.  (Restricted quantifier version.)  (The
     proof was shortened by Andrew Salmon, 30-May-2011.) $)
  rexim $p |- ( A. x e. A ( ph -> ps ) ->
               ( E. x e. A ph -> E. x e. A ps ) ) $=
    ( wi wral wn wrex con3 ral2imi con3d ralnex con2bii 3imtr4g ) ABEZCDFZAGZCD
    FZGBGZCDFZGACDHZBCDHZPTROSQCDABIJKRUAACDLMTUBBCDLMN $.
    $( [30-May-2011] $) $( [22-Nov-1994] $)

  $( Theorem 19.22 of [Margaris] p. 90.  (Restricted quantifier version.) $)
  reximOLD $p |- ( A. x e. A ( ph -> ps ) ->
               ( E. x e. A ph -> E. x e. A ps ) ) $=
    ( cv wcel wi wal wa wex wral wrex imdistan albii exim sylbi df-ral df-rex
    imbi12i 3imtr4i ) CEDFZABGZGZCHZUAAIZCJZUABIZCJZGZUBCDKACDLZBCDLZGUDUEUGGZC
    HUIUCULCUAABMNUEUGCOPUBCDQUJUFUKUHACDRBCDRST $.
    $( [22-Nov-1994] $)

  ${
    reximia.1 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Inference quantifying both antecedent and consequent. $)
    reximia $p |- ( E. x e. A ph -> E. x e. A ps ) $=
      ( wi wrex rexim mprg ) ABFACDGBCDGFCDABCDHEI $.
      $( [10-Feb-1997] $)
  $}

  ${
    reximi2.1 $e |- ( ( x e. A /\ ph ) -> ( x e. B /\ ps ) ) $.
    $( Inference quantifying both antecedent and consequent, based on Theorem
       19.22 of [Margaris] p. 90. $)
    reximi2 $p |- ( E. x e. A ph -> E. x e. B ps ) $=
      ( cv wcel wa wex wrex eximi df-rex 3imtr4i ) CGZDHAIZCJOEHBIZCJACDKBCEKPQ
      CFLACDMBCEMN $.
      $( [8-Nov-2004] $)
  $}

  ${
    reximi.1 $e |- ( ph -> ps ) $.
    $( Inference quantifying both antecedent and consequent. $)
    reximi $p |- ( E. x e. A ph -> E. x e. A ps ) $=
      ( wi cv wcel a1i reximia ) ABCDABFCGDHEIJ $.
      $( [18-Oct-1996] $)
  $}

  ${
    reximdai.1 $e |- ( ph -> A. x ph ) $.
    reximdai.2 $e |- ( ph -> ( x e. A -> ( ps -> ch ) ) ) $.
    $( Deduction from Theorem 19.22 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    reximdai $p |- ( ph -> ( E. x e. A ps -> E. x e. A ch ) ) $=
      ( wi wral wrex r19.21ai rexim syl ) ABCHZDEIBDEJCDEJHANDEFGKBCDELM $.
      $( [31-Aug-1999] $)
  $}

  ${
    $d x ph $.
    reximdv2.1 $e |- ( ph -> ( ( x e. A /\ ps ) -> ( x e. B /\ ch ) ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.22 of [Margaris] p. 90. $)
    reximdv2 $p |- ( ph -> ( E. x e. A ps -> E. x e. B ch ) ) $=
      ( cv wcel wa wex wrex eximdv df-rex 3imtr4g ) ADHZEIBJZDKPFICJZDKBDELCDFL
      AQRDGMBDENCDFNO $.
      $( [17-Sep-2003] $)
  $}

  ${
    $d x ph $.
    reximdvai.1 $e |- ( ph -> ( x e. A -> ( ps -> ch ) ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.22 of [Margaris] p. 90. $)
    reximdvai $p |- ( ph -> ( E. x e. A ps -> E. x e. A ch ) ) $=
      ( ax-17 reximdai ) ABCDEADGFH $.
      $( [14-Nov-2002] $)
  $}

  ${
    $d x ph $.
    reximdv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Deduction from Theorem 19.22 of [Margaris] p. 90.  (Restricted
       quantifier version with strong hypothesis.) $)
    reximdv $p |- ( ph -> ( E. x e. A ps -> E. x e. A ch ) ) $=
      ( wi cv wcel a1d reximdvai ) ABCDEABCGDHEIFJK $.
      $( [24-Jun-1998] $)
  $}

  ${
    $d x ph $.
    reximdva.1 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Deduction quantifying both antecedent and consequent, based on Theorem
       19.22 of [Margaris] p. 90. $)
    reximdva $p |- ( ph -> ( E. x e. A ps -> E. x e. A ch ) ) $=
      ( cv wcel wi ex reximdvai ) ABCDEADGEHBCIFJK $.
      $( [22-May-1999] $)
  $}

  ${
    $d x y $.  $d y A $.  $d x B $.
    $( Theorem 19.12 of [Margaris] p. 89 with restricted quantifiers.  (The
       proof was shortened by Andrew Salmon, 30-May-2011.) $)
    r19.12 $p |- ( E. x e. A A. y e. B ph -> A. y e. B E. x e. A ph ) $=
      ( wral wrex cv wcel ax-17 hbra1 hbrex ax-1 r19.21ai ra4 com12 reximdv
      ralimia syl ) ACEFZBDGZUACEFABDGZCEFUAUACETCBDBHDICJACEKLUACHEIZMNUAUBCEU
      CTABDTUCAACEOPQRS $.
      $( [30-May-2011] $) $( [15-Oct-2003] $)

    $( Theorem 19.12 of [Margaris] p. 89 with restricted quantifiers. $)
    r19.12OLD $p |- ( E. x e. A A. y e. B ph -> A. y e. B E. x e. A ph ) $=
      ( wral wrex cv wcel ax-17 hbra1 hbrex ax-1 r19.21ai wi ra4 com12 a1d
      reximdvai ralimia syl ) ACEFZBDGZUCCEFABDGZCEFUCUCCEUBCBDBHDIZCJACEKLUCCH
      EIZMNUCUDCEUFUBABDUFUBAOUEUBUFAACEPQRSTUA $.
      $( [15-Oct-2003] $)
  $}

  ${
    r19.23.1 $e |- ( ps -> A. x ps ) $.
    $( Theorem 19.23 of [Margaris] p. 90 with restricted quantifiers. $)
    r19.23 $p |- ( A. x e. A ( ph -> ps ) <-> ( E. x e. A ph -> ps ) ) $=
      ( cv wcel wi wal wa wex wral wrex impexp albii 19.23 bitr3i df-ral
      df-rex imbi1i 3bitr4i ) CFDGZABHZHZCIZUBAJZCKZBHZUCCDLACDMZBHUEUFBHZCIUHU
      JUDCUBABNOUFBCEPQUCCDRUIUGBACDSTUA $.
      $( [25-May-2011] $) $( [22-Oct-2010] $)

    $( Theorem 19.23 of [Margaris] p. 90 with restricted quantifiers. $)
    r19.23OLD $p |- ( A. x e. A ( ph -> ps ) <-> ( E. x e. A ph -> ps ) ) $=
      ( cv wcel wa wi wal wrex wral impexp albii wex df-rex imbi1i 19.23
      bitr4i df-ral 3bitr4ri ) CFDGZAHZBIZCJZUBABIZIZCJACDKZBIZUFCDLUDUGCUBABMN
      UIUCCOZBIUEUHUJBACDPQUCBCERSUFCDTUA $.
      $( [22-Oct-2010] $)
  $}

  ${
    $d x ps $.
    $( Theorem 19.23 of [Margaris] p. 90 with restricted quantifiers. $)
    r19.23v $p |- ( A. x e. A ( ph -> ps ) <-> ( E. x e. A ph -> ps ) ) $=
      ( ax-17 r19.23 ) ABCDBCEF $.
      $( [31-Aug-1999] $)
  $}

  ${
    r19.23ai.1 $e |- ( ps -> A. x ps ) $.
    r19.23ai.2 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.)  (The proof was shortened by Andrew Salmon,
       30-May-2011.) $)
    r19.23ai $p |- ( E. x e. A ph -> ps ) $=
      ( wi wral wrex rgen r19.23 mpbi ) ABGZCDHACDIBGMCDFJABCDEKL $.
      $( [30-May-2011] $) $( [30-Nov-2003] $)

    $( Inference from Theorem 19.21 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.23aiOLD $p |- ( E. x e. A ph -> ps ) $=
      ( wrex cv wcel wa wex df-rex imp 19.23ai sylbi ) ACDGCHDIZAJZCKBACDLQBCEP
      ABFMNO $.
      $( [30-Nov-2003] $)
  $}

  ${
    $d x ps $.
    r19.23aiv.1 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.23aiv $p |- ( E. x e. A ph -> ps ) $=
      ( ax-17 r19.23ai ) ABCDBCFEG $.
      $( [20-Nov-1994] $)
  $}

  ${
    $d x ps $.
    r19.23aiva.1 $e |- ( ( x e. A /\ ph ) -> ps ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version). $)
    r19.23aiva $p |- ( E. x e. A ph -> ps ) $=
      ( cv wcel ex r19.23aiv ) ABCDCFDGABEHI $.
      $( [18-Dec-2006] $)
  $}

  ${
    r19.23ad.1 $e |- ( ph -> A. x ph ) $.
    r19.23ad.2 $e |- ( ch -> A. x ch ) $.
    r19.23ad.3 $e |- ( ph -> ( x e. A -> ( ps -> ch ) ) ) $.
    $( Deduction from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version).  (The proof was shortened by Andrew Salmon, 30-May-2011.) $)
    r19.23ad $p |- ( ph -> ( E. x e. A ps -> ch ) ) $=
      ( wi wral wrex r19.21ai r19.23 sylib ) ABCIZDEJBDEKCIAODEFHLBCDEGMN $.
      $( [30-May-2011] $) $( [27-May-1998] $)

    $( Deduction from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version). $)
    r19.23adOLD $p |- ( ph -> ( E. x e. A ps -> ch ) ) $=
      ( cv wcel wa wex wrex imp3a 19.23ad df-rex syl5ib ) ADIEJZBKZDLCBDEMASCDF
      GARBCHNOBDEPQ $.
      $( [27-May-1998] $)
  $}

  ${
    $d x ph $.  $d x ch $.
    r19.23adv.1 $e |- ( ph -> ( x e. A -> ( ps -> ch ) ) ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version).  (The proof was shortened by Eric Schmidt, 22-Dec-2006.) $)
    r19.23adv $p |- ( ph -> ( E. x e. A ps -> ch ) ) $=
      ( ax-17 r19.23ad ) ABCDEADGCDGFH $.
      $( [22-Dec-2006] $) $( [14-Nov-2002] $)
  $}

  ${
    $d x ph $.  $d x ch $.
    r19.23adva.1 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version). $)
    r19.23adva $p |- ( ph -> ( E. x e. A ps -> ch ) ) $=
      ( cv wcel wi ex r19.23adv ) ABCDEADGEHBCIFJK $.
      $( [20-Jan-2007] $)
  $}

  ${
    $d x y ps $.  $d y A $.
    r19.23aivv.1 $e |- ( ( x e. A /\ y e. B ) -> ( ph -> ps ) ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90 (restricted quantifier
       version). $)
    r19.23aivv $p |- ( E. x e. A E. y e. B ph -> ps ) $=
      ( wrex cv wcel r19.23adva r19.23aiv ) ADFHBCECIEJABDFGKL $.
      $( [17-Feb-2004] $)
  $}

  ${
    $d x y ph $.  $d x y ch $.  $d y A $.
    r19.23advv.1 $e |- ( ph -> ( ( x e. A /\ y e. B ) -> ( ps -> ch ) ) ) $.
    $( Inference from Theorem 19.23 of [Margaris] p. 90.  (Restricted
       quantifier version.) $)
    r19.23advv $p |- ( ph -> ( E. x e. A E. y e. B ps -> ch ) ) $=
      ( wrex cv wcel wa wi expdimp r19.23adv r19.23adva ) ABEGICDFADJFKZLBCEGAQ
      EJGKBCMHNOP $.
      $( [22-Jul-2004] $)
  $}

  $( Theorem 19.26 of [Margaris] p. 90 with restricted quantifiers.  (The proof
     was shortened by Andrew Salmon, 30-May-2011.) $)
  r19.26 $p |- ( A. x e. A ( ph /\ ps ) <->
               ( A. x e. A ph /\ A. x e. A ps ) ) $=
    ( wa wral pm3.26 ralimi pm3.27 jca pm3.2 ral2imi imp impbii ) ABEZCDFZACDFZ
    BCDFZEPQROACDABGHOBCDABIHJQRPABOCDABKLMN $.
    $( [30-May-2011] $) $( [28-Jan-1997] $)

  $( Theorem 19.26 of [Margaris] p. 90 with restricted quantifiers. $)
  r19.26OLD $p |- ( A. x e. A ( ph /\ ps ) <->
               ( A. x e. A ph /\ A. x e. A ps ) ) $=
    ( cv wcel wa wi wal wral jcab albii 19.26 bitri df-ral anbi12i 3bitr4i ) CE
    DFZABGZHZCIZRAHZCIZRBHZCIZGZSCDJACDJZBCDJZGUAUBUDGZCIUFTUICRABKLUBUDCMNSCDO
    UGUCUHUEACDOBCDOPQ $.
    $( [28-Jan-1997] $)

  $( Theorem 19.26 of [Margaris] p. 90 with 2 restricted quantifiers. $)
  r19.26-2 $p |- ( A. x e. A A. y e. B ( ph /\ ps ) <->
               ( A. x e. A A. y e. B ph /\ A. x e. A A. y e. B ps ) ) $=
    ( wa wral r19.26 ralbii bitri ) ABGDFHZCEHADFHZBDFHZGZCEHMCEHNCEHGLOCEABDFI
    JMNCEIK $.
    $( [10-Aug-2004] $)

  $( Theorem 19.26 of [Margaris] p. 90 with mixed quantifiers. $)
  r19.26m $p |- ( A. x ( ( x e. A -> ph ) /\ ( x e. B -> ps ) ) <->
               ( A. x e. A ph /\ A. x e. B ps ) ) $=
    ( cv wcel wi wa wal wral 19.26 df-ral anbi12i bitr4i ) CFZDGAHZPEGBHZICJQCJ
    ZRCJZIACDKZBCEKZIQRCLUASUBTACDMBCEMNO $.
    $( [22-Feb-2004] $)

  $( Distribute a restricted universal quantifier over a biconditional.
     Theorem 19.15 of [Margaris] p. 90 with restricted quantification. $)
  ralbi $p |- ( A. x e. A ( ph <-> ps ) ->
               ( A. x e. A ph <-> A. x e. A ps ) ) $=
    ( wb wral hbra1 cv wcel ra4 imp ralbida ) ABEZCDFZABCDMCDGNCHDIMMCDJKL $.
    $( [6-Oct-2003] $)

  ${
    $d x ps $.
    $( Restricted version of one direction of Theorem 19.27 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.)  (The
       proof was shortened by Andrew Salmon, 30-May-2011.) $)
    r19.27av $p |- ( ( A. x e. A ph /\ ps ) -> A. x e. A ( ph /\ ps ) ) $=
      ( wral wa cv wcel ax-1 r19.21aiv anim2i r19.26 sylibr ) ACDEZBFNBCDEZFABF
      CDEBONBBCDBCGDHIJKABCDLM $.
      $( [30-May-2011] $) $( [3-Jun-2004] $)

    $( Restricted version of one direction of Theorem 19.27 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.) $)
    r19.27avOLD $p |- ( ( A. x e. A ph /\ ps ) -> A. x e. A ( ph /\ ps ) ) $=
      ( cv wcel wi wa wal wral pm2.27 anim1d com12 alimi df-ral anbi1i 19.27v
      bitr4i 3imtr4i ) CEDFZAGZBHZCIZTABHZGZCIACDJZBHZUDCDJUBUECTUBUDTUAABTAKLM
      NUGUACIZBHUCUFUHBACDOPUABCQRUDCDOS $.
      $( [3-Jun-2004] $)
  $}

  ${
    $d x ph $.
    $( Restricted version of one direction of Theorem 19.28 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.) $)
    r19.28av $p |- ( ( ph /\ A. x e. A ps ) -> A. x e. A ( ph /\ ps ) ) $=
      ( wral wa r19.27av ancom ralbii 3imtr4i ) BCDEZAFBAFZCDEAKFABFZCDEBACDGAK
      HMLCDABHIJ $.
      $( [2-Apr-2004] $)
  $}

  $( Theorem 19.29 of [Margaris] p. 90 with restricted quantifiers.  (The proof
     was shortened by Andrew Salmon, 30-May-2011.) $)
  r19.29 $p |- ( ( A. x e. A ph /\ E. x e. A ps ) ->
               E. x e. A ( ph /\ ps ) ) $=
    ( wral wrex wa wi pm3.2 ralimi rexim syl imp ) ACDEZBCDFZABGZCDFZNBPHZCDEOQ
    HARCDABIJBPCDKLM $.
    $( [30-May-2011] $) $( [31-Aug-1999] $)

  $( Theorem 19.29 of [Margaris] p. 90 with restricted quantifiers. $)
  r19.29OLD $p |- ( ( A. x e. A ph /\ E. x e. A ps ) ->
               E. x e. A ( ph /\ ps ) ) $=
    ( cv wcel wi wal wa wex wral wrex 19.29 anandi abai anbi1i bitr4i an12
    3bitri exbii sylibr df-ral df-rex anbi12i 3imtr4i ) CEDFZAGZCHZUFBIZCJZIZUF
    ABIZIZCJZACDKZBCDLZIULCDLUKUGUIIZCJUNUGUICMUMUQCUMUFAIZUIIZUFUGBIIZUQUFABNU
    SUFUGIZUIIUTURVAUIUFAOPUFUGBNQUFUGBRSTUAUOUHUPUJACDUBBCDUCUDULCDUCUE $.
    $( [31-Aug-1999] $)

  $( Variation of Theorem 19.29 of [Margaris] p. 90 with restricted
     quantifiers. $)
  r19.29r $p |- ( ( E. x e. A ph /\ A. x e. A ps ) ->
                E. x e. A ( ph /\ ps ) ) $=
    ( wral wrex wa r19.29 ancom rexbii 3imtr4i ) BCDEZACDFZGBAGZCDFMLGABGZCDFBA
    CDHMLIONCDABIJK $.
    $( [31-Aug-1999] $)

  ${
    $d x ph $.
    $( Theorem 19.32 of [Margaris] p. 90 with restricted quantifiers. $)
    r19.32v $p |- ( A. x e. A ( ph \/ ps ) <-> ( ph \/ A. x e. A ps ) ) $=
      ( wn wi wral wo r19.21v df-or ralbii 3bitr4i ) AEZBFZCDGMBCDGZFABHZCDGAOH
      MBCDIPNCDABJKAOJL $.
      $( [25-Nov-2003] $)
  $}

  $( Restricted quantifier version of Theorem 19.35 of [Margaris] p. 90. $)
  r19.35 $p |- ( E. x e. A ( ph -> ps ) <->
               ( A. x e. A ph -> E. x e. A ps ) ) $=
    ( wral wn wi wrex wa r19.26 annim ralbii df-an 3bitr3i con2bii dfrex2
    imbi2i 3bitr4ri ) ACDEZBFZCDEZFZGZABGZFZCDEZFSBCDHZGUDCDHUFUCATIZCDESUAIUFU
    CFATCDJUHUECDABKLSUAMNOUGUBSBCDPQUDCDPR $.
    $( [20-Sep-2003] $)

  ${
    $d x ps $.
    $( One direction of a restricted quantifier version of Theorem 19.36 of
       [Margaris] p. 90.  The other direction doesn't hold when ` A ` is
       empty. $)
    r19.36av $p |- ( E. x e. A ( ph -> ps ) -> ( A. x e. A ph -> ps ) ) $=
      ( wi wrex wral r19.35 cv wcel idd r19.23aiv imim2i sylbi ) ABECDFACDGZBCD
      FZEOBEABCDHPBOBBCDCIDJBKLMN $.
      $( [22-Oct-2003] $)
  $}

  ${
    $d x ph $.
    $( Restricted version of one direction of Theorem 19.37 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.) $)
    r19.37av $p |- ( E. x e. A ( ph -> ps ) -> ( ph -> E. x e. A ps ) ) $=
      ( wi wrex wral r19.35 cv wcel ax-1 r19.21aiv imim1i sylbi ) ABECDFACDGZBC
      DFZEAPEABCDHAOPAACDACIDJKLMN $.
      $( [2-Apr-2004] $)
  $}

  $( Restricted quantifier version of Theorem 19.40 of [Margaris] p. 90. $)
  r19.40 $p |- ( E. x e. A ( ph /\ ps ) ->
               ( E. x e. A ph /\ E. x e. A ps ) ) $=
    ( wa wrex pm3.26 reximi pm3.27 jca ) ABEZCDFACDFBCDFKACDABGHKBCDABIHJ $.
    $( [2-Apr-2004] $)

  ${
    r19.41.1 $e |- ( ps -> A. x ps ) $.
    $( Restricted quantifier version of Theorem 19.41 of [Margaris] p. 90. $)
    r19.41 $p |- ( E. x e. A ( ph /\ ps ) <-> ( E. x e. A ph /\ ps ) ) $=
      ( cv wcel wa wex wrex anass exbii 19.41 bitr3i df-rex anbi1i 3bitr4i ) CF
      DGZABHZHZCIZRAHZCIZBHZSCDJACDJZBHUAUBBHZCIUDUFTCRABKLUBBCEMNSCDOUEUCBACDO
      PQ $.
      $( [1-Nov-2010] $)
  $}

  ${
    $d x ps $.
    $( Restricted quantifier version of Theorem 19.41 of [Margaris] p. 90. $)
    r19.41v $p |- ( E. x e. A ( ph /\ ps ) <-> ( E. x e. A ph /\ ps ) ) $=
      ( ax-17 r19.41 ) ABCDBCEF $.
      $( [17-Dec-2003] $)
  $}

  ${
    $d x ph $.
    $( Restricted version of Theorem 19.42 of [Margaris] p. 90. $)
    r19.42v $p |- ( E. x e. A ( ph /\ ps ) <-> ( ph /\ E. x e. A ps ) ) $=
      ( wa wrex r19.41v ancom rexbii 3bitr4i ) BAEZCDFBCDFZAEABEZCDFALEBACDGMKC
      DABHIALHJ $.
      $( [27-May-1998] $)
  $}

  $( Restricted version of Theorem 19.43 of [Margaris] p. 90.  (The proof was
     shortened by Andrew Salmon, 30-May-2011.) $)
  r19.43 $p |- ( E. x e. A ( ph \/ ps ) <->
               ( E. x e. A ph \/ E. x e. A ps ) ) $=
    ( wn wi wrex wral wo r19.35 df-or rexbii ralnex imbi1i bitr4i 3bitr4i ) AEZ
    BFZCDGQCDHZBCDGZFZABIZCDGACDGZTIZQBCDJUBRCDABKLUDUCEZTFUAUCTKSUETACDMNOP $.
    $( [30-May-2011] $) $( [27-May-1998] $)

  $( Restricted version of Theorem 19.43 of [Margaris] p. 90. $)
  r19.43OLD $p |- ( E. x e. A ( ph \/ ps ) <->
               ( E. x e. A ph \/ E. x e. A ps ) ) $=
    ( cv wcel wo wa wex wrex andi exbii 19.43 bitri df-rex orbi12i 3bitr4i ) CE
    DFZABGZHZCIZRAHZCIZRBHZCIZGZSCDJACDJZBCDJZGUAUBUDGZCIUFTUICRABKLUBUDCMNSCDO
    UGUCUHUEACDOBCDOPQ $.
    $( [27-May-1998] $)

  ${
    $d x ps $.
    $( One direction of a restricted quantifier version of Theorem 19.44 of
       [Margaris] p. 90.  The other direction doesn't hold when ` A ` is
       empty. $)
    r19.44av $p |- ( E. x e. A ( ph \/ ps ) -> ( E. x e. A ph \/ ps ) ) $=
      ( wo wrex r19.43 cv wcel idd r19.23aiv orim2i sylbi ) ABECDFACDFZBCDFZENB
      EABCDGOBNBBCDCHDIBJKLM $.
      $( [2-Apr-2004] $)
  $}

  ${
    $d x ph $.
    $( Restricted version of one direction of Theorem 19.45 of [Margaris]
       p. 90.  (The other direction doesn't hold when ` A ` is empty.) $)
    r19.45av $p |- ( E. x e. A ( ph \/ ps ) -> ( ph \/ E. x e. A ps ) ) $=
      ( wo wrex r19.43 cv wcel idd r19.23aiv orim1i sylbi ) ABECDFACDFZBCDFZEAO
      EABCDGNAOAACDCHDIAJKLM $.
      $( [2-Apr-2004] $)
  $}

  ${
    $d x y $.  $d x B $.  $d y A $.
    $( Commutation of restricted quantifiers. $)
    ralcom $p |- ( A. x e. A A. y e. B ph <-> A. y e. B A. x e. A ph ) $=
      ( cv wcel wa wi wal wral ancom imbi1i 2albii alcom bitri r2al 3bitr4i )
      BFDGZCFEGZHZAIZCJBJZTSHZAIZBJCJZACEKBDKABDKCEKUCUECJBJUFUBUEBCUAUDASTLMNU
      EBCOPABCDEQACBEDQR $.
      $( [13-Oct-1999] $)
  $}

  ${
    $d x y $.  $d x B $.  $d y A $.
    $( Commutation of restricted quantifiers. $)
    rexcom $p |- ( E. x e. A E. y e. B ph <-> E. y e. B E. x e. A ph ) $=
      ( cv wcel wa wex wrex ancom anbi1i 2exbii excom bitri r2ex 3bitr4i ) BFDG
      ZCFEGZHZAHZCIBIZSRHZAHZBICIZACEJBDJABDJCEJUBUDCIBIUEUAUDBCTUCARSKLMUDBCNO
      ABCDEPACBEDPQ $.
      $( [19-Nov-1995] $)
  $}

  ${
    $d y z A $.  $d x z A $.
    $( Commutation of restricted quantifiers.  Note that ` x ` and ` y `
       needn't be distinct (this makes the proof longer but illustrates the use
       of ~ dvelim ).  (The proof was shortened by Andrew Salmon,
       30-May-2011.) $)
    ralcom2 $p |- ( A. x e. A A. y e. A ph -> A. y e. A A. x e. A ph ) $=
      ( vz cv wcel wi wal wral wceq wb eleq1 a4s imbi1d dral1 bicomd imbi12d
      biimpd wn hbnae ax-17 dvelim nalequcoms hba1 a1i hbimd hbald ax-4 imim2i
      com23 al2imi syl9 sylsyld pm2.61i df-ral imbi2i albii bitri 3imtr4i ) BFZ
      DGZCFZDGZAHZCIZHZBIZVDVBAHZBIZHZCIZACDJZBDJZABDJZCDJZVAVCKZBIZVHVLHVRVHVL
      VGVKBCVRVBVDVFVJVQVBVDLBVAVCDMNZVRVJVFVIVEBCVRVBVDAVSOPQRPSVRTZVTCIVHVHCI
      VLBCCUAZVTVGCBBCBUAVTVBVFCWAVBVBCIHCBEFZDGZVBCBEWCCUBWBVADMUCUDVFVFCIHVTV
      ECUEUFUGUHVTVHVKCVTVDVDBIVHVJWCVDBCEWCBUBWBVCDMUCVGVDVIBVGVBVDAVFVEVBVECU
      IUJUKULUMULUNUOVNVBVMHZBIVHVMBDUPWDVGBVMVFVBACDUPUQURUSVPVDVOHZCIVLVOCDUP
      WEVKCVOVJVDABDUPUQURUSUT $.
      $( [8-Jun-2011] $) $( [24-Nov-1994] $)

    $( Commutation of restricted quantifiers.  Note that ` x ` and ` y `
       needn't be distinct (this makes the proof longer but illustrates the use
       of ~ dvelim ). $)
    ralcom2OLD $p |- ( A. x e. A A. y e. A ph -> A. y e. A A. x e. A ph ) $=
      ( vz cv wcel wi wal wral wceq id wb eleq1 a4s imbi1d dral1 imbi2d dral2
      imbi12d mpbii wn hbnae hbal ax-17 dvelim nalequcoms hba1 a1i hbimd hbald
      ax-4 imim2i com23 al2imi syl9 syld hbnaes pm2.61i df-ral imbi2i albii
      bitri 3imtr4i ) BFZDGZCFZDGZAHZCIZHZBIZVHVFAHZBIZHZCIZACDJZBDJZABDJZCDJZV
      EVGKZBIZVLVPHZWBVFVNHZBIZWEHWCWELWBWEVLWEVPWDVKBCBWBVNVJVFVMVIBCWBVFVHAWA
      VFVHMBVEVGDNOZPQRSWDVOBCWBVFVHVNWFPQTUAWCBCCWBUBZCIZVLVLCIVPWHVKCBWGBCBCB
      UCUDWGVKVKCIHCWGVFVJCBCCUCVFVFCIHCBEFZDGZVFCBEWJCUEWIVEDNUFUGVJVJCIHWGVIC
      UHUIUJOUKWGVLVOCWGVHVHBIVLVNWJVHBCEWJBUEWIVGDNUFVKVHVMBVKVFVHAVJVIVFVICUL
      UMUNUOUPUOUQURUSVRVFVQHZBIVLVQBDUTWKVKBVQVJVFACDUTVAVBVCVTVHVSHZCIVPVSCDU
      TWLVOCVSVNVHABDUTVAVBVCVD $.
      $( [24-Nov-1994] $)
  $}

  ${
    $( A commutative law for restricted quantifiers that swaps the domain of
       the restriction. $)
    ralcom3 $p |- ( A. x e. A ( x e. B -> ph ) <->
                    A. x e. B ( x e. A -> ph ) ) $=
      ( cv wcel wi wral pm2.04 ralimi2 impbii ) BEZDFZAGZBCHLCFZAGZBDHNPBCDOMAI
      JPNBDCMOAIJK $.
      $( [22-Feb-2004] $)
  $}

  ${
    $d y A $.  $d x B $.  $d x y $.
    reean.1 $e |- ( ph -> A. y ph ) $.
    reean.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange existential quantifiers.  (The proof was shortened by Andrew
       Salmon, 30-May-2011.) $)
    reean $p |- ( E. x e. A E. y e. B ( ph /\ ps ) <->
                 ( E. x e. A ph /\ E. y e. B ps ) ) $=
      ( cv wcel wa wex wrex an4 2exbii ax-17 hban eean bitri r2ex df-rex
      anbi12i 3bitr4i ) CIEJZDIFJZKABKZKZDLCLZUDAKZCLZUEBKZDLZKZUFDFMCEMACEMZBD
      FMZKUHUIUKKZDLCLUMUGUPCDUDUEABNOUIUKCDUDADUDDPGQUEBCUECPHQRSUFCDEFTUNUJUO
      ULACEUABDFUAUBUC $.
      $( [30-May-2011] $) $( [27-Oct-2010] $)

    $( Rearrange existential quantifiers. $)
    reeanOLD $p |- ( E. x e. A E. y e. B ( ph /\ ps ) <->
                 ( E. x e. A ph /\ E. y e. B ps ) ) $=
      ( cv wcel wa wex wrex anass an4 bitr3i 2exbii ax-17 19.42 exbii hban
      eean 3bitr3i df-rex rexbii bitri anbi12i 3bitr4i ) CIEJZDIFJZABKZKZDLZKZC
      LZUIAKZCLZUJBKZDLZKZUKDFMZCEMZACEMZBDFMZKUIULKZDLZCLUPURKZDLCLUOUTVEVGCDV
      EUIUJKUKKVGUIUJUKNUIUJABOPQVFUNCUIULDUIDRZSTUPURCDUIADVHGUAUJBCUJCRHUAUBU
      CVBUMCEMUOVAUMCEUKDFUDUEUMCEUDUFVCUQVDUSACEUDBDFUDUGUH $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d y ph $.  $d x ps $.  $d x y $.  $d y A $.  $d x B $.
    $( Rearrange existential quantifiers. $)
    reeanv $p |- ( E. x e. A E. y e. B ( ph /\ ps ) <->
                 ( E. x e. A ph /\ E. y e. B ps ) ) $=
      ( ax-17 reean ) ABCDEFADGBCGH $.
      $( [9-May-1999] $)
  $}

  ${
    $d ph y $.  $d ps x $.  $d A y $.  $d B x $.  $d x y $.
    $( Distribute quantification over "or".  (Contributed by Jeff Madsen,
       19-Jun-2010.) $)
    2ralor $p |- ( A. x e. A A. y e. B ( ph \/ ps ) <->
                  ( A. x e. A ph \/ A. y e. B ps ) ) $=
      ( wo wral wn wa wrex reeanv ioran rexbii rexnal bitr3i bitri anbi12i
      bitr4i 3bitr3i con4bii ) ABGZDFHZCEHZACEHZBDFHZGZAIZBIZJZDFKZCEKZUHCEKZUI
      DFKZJZUDIZUGIZUHUICDEFLULUCIZCEKUPUKURCEUKUBIZDFKURUSUJDFABMNUBDFOPNUCCEO
      QUOUEIZUFIZJUQUMUTUNVAACEOBDFORUEUFMSTUA $.
      $( [19-Jun-2010] $)
  $}

  ${
    hbreu.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbreu.2 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for restricted uniqueness. $)
    hbreu $p |- ( E! y e. A ph -> A. x E! y e. A ph ) $=
      ( cv wcel wa weu wal wreu hban hbeu df-reu albii 3imtr4i ) CGDHZAIZCJZTBK
      ACDLZUABKSBCRABEFMNACDOZUATBUBPQ $.
      $( [11-Jun-2011] $) $( [30-Oct-2010] $)
  $}

  $( ` x ` is not free in ` E! x e. A ph ` . $)
  hbreu1 $p |- ( E! x e. A ph -> A. x E! x e. A ph ) $=
    ( cv wcel wa weu wal wreu hbeu1 df-reu albii 3imtr4i ) BDCEAFZBGZOBHABCIZPB
    HNBJABCKZPOBQLM $.
    $( [19-Mar-1997] $)

  $( An "identity" law of concretion for restricted abstraction.  Special case
     of Definition 2.1 of [Quine] p. 16. $)
  rabid $p |- ( x e. { x e. A | ph } <-> ( x e. A /\ ph ) ) $=
    ( cv wcel wa crab df-rab abeq2i ) BDCEAFBABCGABCHI $.
    $( [9-Oct-2003] $)

  ${
    $d x A $.
    $( An "identity" law for restricted class abstraction.  (The proof was
       shortened by Andrew Salmon, 30-May-2011.) $)
    rabid2 $p |- ( A = { x e. A | ph } <-> A. x e. A ph ) $=
      ( cv wcel wa cab wceq wi wal crab wral wb abeq2 pm4.71 albii bitr4i
      df-rab eqeq2i df-ral 3bitr4i ) CBDCEZAFZBGZHZUBAIZBJZCABCKZHABCLUEUBUCMZB
      JUGUCBCNUFUIBUBAOPQUHUDCABCRSABCTUA $.
      $( [30-May-2011] $) $( [9-Oct-2003] $)

    $( An "identity" law for restricted class abstraction. $)
    rabid2OLD $p |- ( A = { x e. A | ph } <-> A. x e. A ph ) $=
      ( cv wcel wi wal wa cab wceq wral crab wb pm4.71 albii abeq2 bitr4i
      df-ral df-rab eqeq2i 3bitr4ri ) BDCEZAFZBGZCUBAHZBIZJZABCKCABCLZJUDUBUEMZ
      BGUGUCUIBUBANOUEBCPQABCRUHUFCABCSTUA $.
      $( [9-Oct-2003] $)
  $}

  $( Swap with a membership relation in a restricted class abstraction. $)
  rabswap $p |- { x e. A | x e. B } = { x e. B | x e. A } $=
    ( cv wcel wa cab crab ancom abbii df-rab 3eqtr4i ) ADZBEZMCEZFZAGONFZAGOABH
    NACHPQANOIJOABKNACKL $.
    $( [4-Jul-2005] $)

  ${
    $d x y $.
    $( The abstraction variable in a restricted class abstraction isn't
       free. $)
    hbrab1 $p |- ( y e. { x e. A | ph } -> A. x y e. { x e. A | ph } ) $=
      ( crab cv wcel wa cab df-rab hbab1 hbxfr ) BCABDEBFDGAHZBIABDJMBCKL $.
      $( [19-Mar-1997] $)
  $}

  ${
    $d x y $.  $d x z $.  $d z A $.
    hbrab.1 $e |- ( ph -> A. x ph ) $.
    hbrab.2 $e |- ( z e. A -> A. x z e. A ) $.
    $( A variable not free in a wff remains so in a restricted class
       abstraction.  (Unnecessary distinct variable restrictions were removed
       by Andrew Salmon, 11-Jul-2011.) $)
    hbrab $p |- ( z e. { y e. A | ph } -> A. x z e. { y e. A | ph } ) $=
      ( crab cv wcel wa cab df-rab hblem hban hbab hbxfr ) BDACEHCIEJZAKZCLACEM
      SBCDRABBDCEGNFOPQ $.
      $( [11-Jul-2011] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x ph $.
    reubidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    reubidva $p |- ( ph -> ( E! x e. A ps <-> E! x e. A ch ) ) $=
      ( cv wcel wa weu wreu pm5.32da eubidv df-reu 3bitr4g ) ADGEHZBIZDJPCIZDJB
      DEKCDEKAQRDAPBCFLMBDENCDENO $.
      $( [13-Nov-2004] $)
  $}

  ${
    $d x ph $.
    reubidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building rule for restricted existential quantifier (deduction
       rule). $)
    reubidv $p |- ( ph -> ( E! x e. A ps <-> E! x e. A ch ) ) $=
      ( wb cv wcel adantr reubidva ) ABCDEABCGDHEIFJK $.
      $( [17-Oct-1996] $)
  $}

  ${
    reubiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Formula-building rule for restricted existential quantifier (inference
       rule). $)
    reubiia $p |- ( E! x e. A ph <-> E! x e. A ps ) $=
      ( cv wcel wa weu wreu pm5.32i eubii df-reu 3bitr4i ) CFDGZAHZCIOBHZCIACDJ
      BCDJPQCOABEKLACDMBCDMN $.
      $( [14-Nov-2004] $)
  $}

  ${
    reubii.1 $e |- ( ph <-> ps ) $.
    $( Formula-building rule for restricted existential quantifier (inference
       rule). $)
    reubii $p |- ( E! x e. A ph <-> E! x e. A ps ) $=
      ( wb cv wcel a1i reubiia ) ABCDABFCGDHEIJ $.
      $( [22-Oct-1999] $)
  $}

  ${
    $d y A $.  $d y B $.
    raleq1f.1 $e |- ( y e. A -> A. x y e. A ) $.
    raleq1f.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Equality theorem for restricted universal quantifier, with
       bound-variable hypotheses instead of distinct variable restrictions.
       (Unnecessary distinct variable restrictions were removed by Andrew
       Salmon, 11-Jul-2011.) $)
    raleqf $p |- ( A = B -> ( A. x e. A ph <-> A. x e. B ph ) ) $=
      ( wceq cv wcel wi wal wral hbeq eleq2 imbi1d albid df-ral 3bitr4g ) DEHZB
      IZDJZAKZBLUAEJZAKZBLABDMABEMTUCUEBBCCDEFGNTUBUDADEUAOPQABDRABERS $.
      $( [11-Jul-2011] $) $( [7-Mar-2004] $)

    $( Equality theorem for restricted existential quantifier, with
       bound-variable hypotheses instead of distinct variable restrictions.
       (Unnecessary distinct variable restrictions were removed by Andrew
       Salmon, 11-Jul-2011.) $)
    rexeqf $p |- ( A = B -> ( E. x e. A ph <-> E. x e. B ph ) ) $=
      ( wceq cv wcel wa wex wrex hbeq eleq2 anbi1d exbid df-rex 3bitr4g ) DEHZB
      IZDJZAKZBLUAEJZAKZBLABDMABEMTUCUEBBCCDEFGNTUBUDADEUAOPQABDRABERS $.
      $( [11-Jul-2011] $) $( [9-Oct-2003] $)

    $( Equality theorem for restricted uniqueness quantifier, with
       bound-variable hypotheses instead of distinct variable restrictions.
       (Unnecessary distinct variable restrictions were removed by Andrew
       Salmon, 11-Jul-2011.) $)
    reueq1f $p |- ( A = B -> ( E! x e. A ph <-> E! x e. B ph ) ) $=
      ( wceq cv wcel wa weu wreu hbeq eleq2 anbi1d eubid df-reu 3bitr4g ) DEHZB
      IZDJZAKZBLUAEJZAKZBLABDMABEMTUCUEBBCCDEFGNTUBUDADEUAOPQABDRABERS $.
      $( [11-Jul-2011] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for restricted universal quantifier. $)
    raleq $p |- ( A = B -> ( A. x e. A ph <-> A. x e. B ph ) ) $=
      ( vy cv wcel ax-17 raleqf ) ABECDEFZCGBHJDGBHI $.
      $( [16-Nov-1995] $)

    $( Equality theorem for restricted existential quantifier. $)
    rexeq $p |- ( A = B -> ( E. x e. A ph <-> E. x e. B ph ) ) $=
      ( vy cv wcel ax-17 rexeqf ) ABECDEFZCGBHJDGBHI $.
      $( [29-Oct-1995] $)

    $( Equality theorem for restricted uniqueness quantifier. $)
    reueq1 $p |- ( A = B -> ( E! x e. A ph <-> E! x e. B ph ) ) $=
      ( vy cv wcel ax-17 reueq1f ) ABECDEFZCGBHJDGBHI $.
      $( [5-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    raleq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for restricted universal quantifier. $)
    raleqdv $p |- ( ph -> ( A. x e. A ps <-> A. x e. B ps ) ) $=
      ( wceq wral wb raleq syl ) ADEGBCDHBCEHIFBCDEJK $.
      $( [13-Nov-2005] $)

    $( Equality deduction for restricted existential quantifier. $)
    rexeqdv $p |- ( ph -> ( E. x e. A ps <-> E. x e. B ps ) ) $=
      ( wceq wrex wb rexeq syl ) ADEGBCDHBCEHIFBCDEJK $.
      $( [14-Jan-2007] $)
  $}

  ${
    $d x A $.  $d x B $.
    raleqd.1 $e |- ( A = B -> ( ph <-> ps ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    raleqbi1dv $p |- ( A = B -> ( A. x e. A ph <-> A. x e. B ps ) ) $=
      ( wceq wral raleq ralbidv bitrd ) DEGZACDHACEHBCEHACDEILABCEFJK $.
      $( [16-Nov-1995] $)

    $( Equality deduction for restricted existential quantifier. $)
    rexeqbi1dv $p |- ( A = B -> ( E. x e. A ph <-> E. x e. B ps ) ) $=
      ( wceq wrex rexeq rexbidv bitrd ) DEGZACDHACEHBCEHACDEILABCEFJK $.
      $( [18-Mar-1997] $)

    $( Equality deduction for restricted uniqueness quantifier. $)
    reueqd $p |- ( A = B -> ( E! x e. A ph <-> E! x e. B ps ) ) $=
      ( wceq wreu reueq1 reubidv bitrd ) DEGZACDHACEHBCEHACDEILABCEFJK $.
      $( [5-Apr-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    raleqbidv.1 $e |- ( ph -> A = B ) $.
    raleqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    raleqbidv $p |- ( ph -> ( A. x e. A ps <-> A. x e. B ch ) ) $=
      ( wral raleqdv ralbidv bitrd ) ABDEIBDFICDFIABDEFGJABCDFHKL $.
      $( [22-Jun-2011] $) $( [6-Nov-2007] $)

    $( Equality deduction for restricted universal quantifier. $)
    rexeqbidv $p |- ( ph -> ( E. x e. A ps <-> E. x e. B ch ) ) $=
      ( wrex rexeqdv rexbidv bitrd ) ABDEIBDFICDFIABDEFGJABCDFHKL $.
      $( [22-Jun-2011] $) $( [6-Nov-2007] $)
  $}

  ${
    $d z A w $.  $d w x $.  $d w y $.  $d w z $.  $d w ps $.  $d w ph $.
    cbvralf.1 $e |- ( z e. A -> A. x z e. A ) $.
    cbvralf.2 $e |- ( z e. A -> A. y z e. A ) $.
    cbvralf.3 $e |- ( ph -> A. y ph ) $.
    cbvralf.4 $e |- ( ps -> A. x ps ) $.
    cbvralf.5 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitition.
       (Unnecessary distinct variable restrictions were removed by Andrew
       Salmon, 11-Jul-2011.) $)
    cbvralf $p |- ( A. x e. A ph <-> A. y e. A ps ) $=
      ( vw cv wcel wi wal wral wsbc ax-17 hblem hbs1 hbim wceq eleq1 sbequ12
      imbi12d cbval hbsb sbequ sbie syl6bb bitri df-ral 3bitr4i ) CMZFNZAOZCPZD
      MZFNZBOZDPZACFQBDFQURLMZFNZACVCRZOZLPVBUQVFCLUQLSVDVECCELFGTACLUAUBUOVCUC
      UPVDAVEUOVCFUDACLUEUFUGVFVALDVDVEDDELFHTACLDIUHUBVALSVCUSUCZVDUTVEBVCUSFU
      DVGVEACUSRBALDCUIABCDJKUJUKUFUGULACFUMBDFUMUN $.
      $( [11-Jul-2011] $) $( [7-Mar-2004] $)

    $( Rule used to change bound variables, using implicit substitition.
       (Contributed by FL, 11-Jul-2011.)  (Unnecessary distinct variable
       restrictions were removed by Andrew Salmon, 11-Jul-2011.) $)
    cbvrexf $p |- ( E. x e. A ph <-> E. y e. A ps ) $=
      ( vw cv wcel wa wex wrex wsbc ax-17 hblem hbs1 hban wceq eleq1 sbequ12
      anbi12d cbvex hbsb sbequ sbie syl6bb bitri df-rex 3bitr4i ) CMZFNZAOZCPZD
      MZFNZBOZDPZACFQBDFQURLMZFNZACVCRZOZLPVBUQVFCLUQLSVDVECCELFGTACLUAUBUOVCUC
      UPVDAVEUOVCFUDACLUEUFUGVFVALDVDVEDDELFHTACLDIUHUBVALSVCUSUCZVDUTVEBVCUSFU
      DVGVEACUSRBALDCUIABCDJKUJUKUFUGULACFUMBDFUMUN $.
      $( [11-Jul-2011] $) $( [27-Apr-2008] $)
  $}

  ${
    $d x z A $.  $d y z A $.
    cbvral.1 $e |- ( ph -> A. y ph ) $.
    cbvral.2 $e |- ( ps -> A. x ps ) $.
    cbvral.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitition. $)
    cbvral $p |- ( A. x e. A ph <-> A. y e. A ps ) $=
      ( vz cv wcel ax-17 cbvralf ) ABCDIEIJEKZCLNDLFGHM $.
      $( [11-Jul-2011] $) $( [31-Jul-2003] $)

    $( Rule used to change bound variables, using implicit substitition.  (The
       proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
    cbvrex $p |- ( E. x e. A ph <-> E. y e. A ps ) $=
      ( vz cv wcel ax-17 cbvrexf ) ABCDIEIJEKZCLNDLFGHM $.
      $( [11-Jul-2011] $) $( [31-Jul-2003] $)
  $}

  ${
    $d z y ph $.  $d z x ps $.  $d z x A $.  $d z y A $.
    cbvralv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Change the bound variable of a restricted universal quantifier using
       implicit substitution. $)
    cbvralv $p |- ( A. x e. A ph <-> A. y e. A ps ) $=
      ( ax-17 cbvral ) ABCDEADGBCGFH $.
      $( [11-Jul-2011] $) $( [28-Jan-1997] $)

    $( Change the bound variable of a restricted existential quantifier using
       implicit substitution. $)
    cbvrexv $p |- ( E. x e. A ph <-> E. y e. A ps ) $=
      ( ax-17 cbvrex ) ABCDEADGBCGFH $.
      $( [11-Jul-2011] $) $( [2-Jun-1998] $)

    $( Change the bound variable of a restricted uniqueness quantifier using
       implicit substitution.  (Unnecessary distinct variable restrictions were
       removed by Andrew Salmon, 11-Jul-2011.) $)
    cbvreuv $p |- ( E! x e. A ph <-> E! y e. A ps ) $=
      ( vz cv wcel wa weu wreu wsbc ax-17 sb8eu sban eubii clelsb3 anbi1i hbsb
      hban wceq eleq1 sbequ sbie syl6bb anbi12d cbveu bitri 3bitri df-reu
      3bitr4i ) CHEIZAJZCKZDHZEIZBJZDKZACELBDELUOUNCGHZMZGKUMCUTMZACUTMZJZGKZUS
      UNCGUNGNOVAVDGUMACGPQVEUTEIZVCJZGKUSVDVGGVBVFVCGCERSQVGURGDVFVCDVFDNACGDA
      DNTUAURGNUTUPUBZVFUQVCBUTUPEUCVHVCACUPMBAGDCUDABCDBCNFUEUFUGUHUIUJACEUKBD
      EUKUL $.
      $( [11-Jul-2011] $) $( [5-Apr-2004] $)
  $}

  ${
    $d x A $.  $d z A $.  $d x y B $.  $d z y B $.  $d w B $.  $d z ph $.
    $d y ps $.  $d x ch $.  $d w ch $.
    cbvral2v.1 $e |- ( x = z -> ( ph <-> ch ) ) $.
    cbvral2v.2 $e |- ( y = w -> ( ch <-> ps ) ) $.
    $( Change bound variables of double restricted universal quantification,
       using implicit substitution. $)
    cbvral2v $p |- ( A. x e. A A. y e. B ph <-> A. z e. A A. w e. B ps ) $=
      ( wral cv wceq ralbidv cbvralv ralbii bitri ) AEILZDHLCEILZFHLBGILZFHLSTD
      FHDMFMNACEIJOPTUAFHCBEGIKPQR $.
      $( [11-Jul-2011] $) $( [10-Aug-2004] $)
  $}

  ${
    $d w ph $.  $d z ps $.  $d x ch $.  $d v ch $.  $d y u th $.  $d x A $.
    $d w A $.  $d x y B $.  $d w y B $.  $d v B $.  $d x y z C $.
    $d w y z C $.  $d v z C $.  $d z y C $.  $d z C $.  $d u C $.
    cbvral3v.1 $e |- ( x = w -> ( ph <-> ch ) ) $.
    cbvral3v.2 $e |- ( y = v -> ( ch <-> th ) ) $.
    cbvral3v.3 $e |- ( z = u -> ( th <-> ps ) ) $.
    $( Change bound variables of triple restricted universal quantification,
       using implicit substitution. $)
    cbvral3v $p |- ( A. x e. A A. y e. B A. z e. C ph <->
                     A. w e. A A. v e. B A. u e. C ps ) $=
      ( wral cv wceq 2ralbidv cbvralv cbvral2v ralbii bitri ) AGMQFLQZEKQCGMQFL
      QZHKQBJMQILQZHKQUEUFEHKERHRSACFGLMNTUAUFUGHKCBDFGIJLMOPUBUCUD $.
      $( [11-Jul-2011] $) $( [10-May-2005] $)
  $}

  ${
    rabbiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (inference
       rule). $)
    rabbiia $p |- { x e. A | ph } = { x e. A | ps } $=
      ( cv wcel wa cab crab pm5.32i abbii df-rab 3eqtr4i ) CFDGZAHZCIOBHZCIACDJ
      BCDJPQCOABEKLACDMBCDMN $.
      $( [22-May-1999] $)
  $}

  ${
    $d x ph $.
    rabbidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (deduction
       rule). $)
    rabbidva $p |- ( ph -> { x e. A | ps } = { x e. A | ch } ) $=
      ( cv wcel wa cab crab pm5.32da abbidv df-rab 3eqtr4g ) ADGEHZBIZDJPCIZDJB
      DEKCDEKAQRDAPBCFLMBDENCDENO $.
      $( [7-Feb-2007] $) $( [28-Nov-2003] $)
  $}

  ${
    $d x ph $.
    rabbidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (deduction
       rule). $)
    rabbidv $p |- ( ph -> { x e. A | ps } = { x e. A | ch } ) $=
      ( wb cv wcel adantr rabbidva ) ABCDEABCGDHEIFJK $.
      $( [10-Feb-1995] $)
  $}

  ${
    $d y A $.  $d y B $.
    rabeqf.1 $e |- ( y e. A -> A. x y e. A ) $.
    rabeqf.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Equality theorem for restricted class abstractions, with bound-variable
       hypotheses instead of distinct variable restrictions. $)
    rabeqf $p |- ( A = B -> { x e. A | ph } = { x e. B | ph } ) $=
      ( wceq cv wcel wa cab crab hbeq eleq2 anbi1d abbid df-rab 3eqtr4g ) DEHZB
      IZDJZAKZBLUAEJZAKZBLABDMABEMTUCUEBBCCDEFGNTUBUDADEUAOPQABDRABERS $.
      $( [11-Jul-2011] $) $( [7-Mar-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for restricted class abstractions. $)
    rabeq $p |- ( A = B -> { x e. A | ph } = { x e. B | ph } ) $=
      ( vy cv wcel ax-17 rabeqf ) ABECDEFZCGBHJDGBHI $.
      $( [15-Oct-2003] $)
  $}

  ${
    $d A x $.  $d B x $.  $d ph x $.
    rabeqbidv.1 $e |- ( ph -> A = B ) $.
    rabeqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality of restricted class abstractions.  (Contributed by Jeff Madsen,
       1-Dec-2009.) $)
    rabeqbidv $p |- ( ph -> { x e. A | ps } = { x e. B | ch } ) $=
      ( crab wceq rabeq syl rabbidv eqtrd ) ABDEIZBDFIZCDFIAEFJOPJGBDEFKLABCDFH
      MN $.
      $( [15-Sep-2011] $) $( [1-Dec-2009] $)
  $}

  ${
    rabeqi.1 $e |- A = { x e. B | ph } $.
    $( Inference rule from equality of a class variable and a restricted class
       abstraction. $)
    rabeq2i $p |- ( x e. A <-> ( x e. B /\ ph ) ) $=
      ( cv wcel crab wa eleq2i rabid bitri ) BFZCGMABDHZGMDGAICNMEJABDKL $.
      $( [16-Feb-2004] $)
  $}

