$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Negated equality and membership
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new connectives. $)
  $c =/= $. $( Not equal to (equal sign with slash through it). $)
  $c e/ $. $( Not an element of (epsilon with slash through it). $)

  $( Extend wff notation to include inequality. $)
  wne $a wff A =/= B $.

  $( Extend wff notation to include negated membership. $)
  wnel $a wff A e/ B $.

  $( Define inequality. $)
  df-ne $a |- ( A =/= B <-> -. A = B ) $.

  $( Define negated membership. $)
  df-nel $a |- ( A e/ B <-> -. A e. B ) $.

  $( Negation of inequality. $)
  nne $p |- ( -. A =/= B <-> A = B ) $=
    ( wceq wne wn df-ne con2bii bicomi ) ABCZABDZEJIABFGH $.
    $( [20-Jun-2006] $) $( [9-Jun-2006] $)

  $( Equality theorem for inequality. $)
  neeq1 $p |- ( A = B -> ( A =/= C <-> B =/= C ) ) $=
    ( wceq wn wne eqeq1 notbid df-ne 3bitr4g ) ABDZACDZEBCDZEACFBCFKLMABCGHACIB
    CIJ $.
    $( [19-Nov-1994] $)

  $( Equality theorem for inequality. $)
  neeq2 $p |- ( A = B -> ( C =/= A <-> C =/= B ) ) $=
    ( wceq wn wne eqeq2 notbid df-ne 3bitr4g ) ABDZCADZECBDZECAFCBFKLMABCGHCAIC
    BIJ $.
    $( [19-Nov-1994] $)

  ${
    neeq1i.1 $e |- A = B $.
    $( Inference for inequality. $)
    neeq1i $p |- ( A =/= C <-> B =/= C ) $=
      ( wceq wne wb neeq1 ax-mp ) ABEACFBCFGDABCHI $.
      $( [3-May-2005] $) $( [29-Apr-2005] $)

    $( Inference for inequality. $)
    neeq2i $p |- ( C =/= A <-> C =/= B ) $=
      ( wceq wne wb neeq2 ax-mp ) ABECAFCBFGDABCHI $.
      $( [17-May-2005] $) $( [29-Apr-2005] $)
  $}

  ${
    neeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction for inequality. $)
    neeq1d $p |- ( ph -> ( A =/= C <-> B =/= C ) ) $=
      ( wceq wne wb neeq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $( [25-Oct-1999] $)

    $( Deduction for inequality. $)
    neeq2d $p |- ( ph -> ( C =/= A <-> C =/= B ) ) $=
      ( wceq wne wb neeq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [25-Oct-1999] $)
  $}

  ${
    necon3abii.1 $e |- ( A = B <-> ph ) $.
    $( Deduction from equality to inequality. $)
    necon3abii $p |- ( A =/= B <-> -. ph ) $=
      ( wne wceq wn df-ne notbii bitri ) BCEBCFZGAGBCHKADIJ $.
      $( [11-Nov-2007] $) $( [9-Nov-2007] $)
  $}

  ${
    necon3bbii.1 $e |- ( ph <-> A = B ) $.
    $( Deduction from equality to inequality. $)
    necon3bbii $p |- ( -. ph <-> A =/= B ) $=
      ( wne wn wceq bicomi necon3abii ) BCEAFABCABCGDHIH $.
      $( [17-Apr-2007] $) $( [13-Apr-2007] $)
  $}

  ${
    necon3bii.1 $e |- ( A = B <-> C = D ) $.
    $( Inference from equality to inequality. $)
    necon3bii $p |- ( A =/= B <-> C =/= D ) $=
      ( wne wceq wn necon3abii df-ne bitr4i ) ABFCDGZHCDFLABEICDJK $.
      $( [25-Feb-2005] $) $( [23-Feb-2005] $)
  $}

  ${
    necon3abid.1 $e |- ( ph -> ( A = B <-> ps ) ) $.
    $( Deduction from equality to inequality. $)
    necon3abid $p |- ( ph -> ( A =/= B <-> -. ps ) ) $=
      ( wceq wn wne notbid df-ne syl5bb ) ACDFZGBGCDHALBEICDJK $.
      $( [24-Mar-2007] $) $( [21-Mar-2007] $)
  $}

  ${
    necon3bbid.1 $e |- ( ph -> ( ps <-> A = B ) ) $.
    $( Deduction from equality to inequality. $)
    necon3bbid $p |- ( ph -> ( -. ps <-> A =/= B ) ) $=
      ( wne wn wceq bicomd necon3abid ) ACDFBGABCDABCDHEIJI $.
      $( [4-Jun-2007] $) $( [2-Jun-2007] $)
  $}

  ${
    necon3bid.1 $e |- ( ph -> ( A = B <-> C = D ) ) $.
    $( Deduction from equality to inequality. $)
    necon3bid $p |- ( ph -> ( A =/= B <-> C =/= D ) ) $=
      ( wne wceq wn necon3abid df-ne syl6bbr ) ABCGDEHZIDEGAMBCFJDEKL $.
      $( [25-Feb-2005] $) $( [23-Feb-2005] $)
  $}

  ${
    necon3ad.1 $e |- ( ph -> ( ps -> A = B ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon3ad $p |- ( ph -> ( A =/= B -> -. ps ) ) $=
      ( wceq wn wne con3d df-ne syl5ib ) ACDFZGBGCDHABLEICDJK $.
      $( [2-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    necon3bd.1 $e |- ( ph -> ( A = B -> ps ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon3bd $p |- ( ph -> ( -. ps -> A =/= B ) ) $=
      ( wn wceq wne con3d df-ne syl6ibr ) ABFCDGZFCDHALBEICDJK $.
      $( [3-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    necon3d.1 $e |- ( ph -> ( A = B -> C = D ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon3d $p |- ( ph -> ( C =/= D -> A =/= B ) ) $=
      ( wne wceq wn necon3ad df-ne syl6ibr ) ADEGBCHZIBCGAMDEFJBCKL $.
      $( [23-Jun-2006] $) $( [10-Jun-2006] $)
  $}

  ${
    necon3i.1 $e |- ( A = B -> C = D ) $.
    $( Contrapositive inference for inequality. $)
    necon3i $p |- ( C =/= D -> A =/= B ) $=
      ( wceq wi wne id necon3d ax-mp ) ABFCDFGZCDHABHGELABCDLIJK $.
      $( [15-Oct-2006] $) $( [9-Aug-2006] $)
  $}

  ${
    necon3ai.1 $e |- ( ph -> A = B ) $.
    $( Contrapositive inference for inequality. $)
    necon3ai $p |- ( A =/= B -> -. ph ) $=
      ( wne wceq wn df-ne con3i sylbi ) BCEBCFZGAGBCHAKDIJ $.
      $( [23-May-2007] $) $( [23-May-2007] $)
  $}

  ${
    necon3bi.1 $e |- ( A = B -> ph ) $.
    $( Contrapositive inference for inequality. $)
    necon3bi $p |- ( -. ph -> A =/= B ) $=
      ( wn wceq wne con3i df-ne sylibr ) AEBCFZEBCGKADHBCIJ $.
      $( [5-Jun-2007] $) $( [1-Jun-2007] $)
  $}

  ${
    necon1ai.1 $e |- ( -. ph -> A = B ) $.
    $( Contrapositive inference for inequality. $)
    necon1ai $p |- ( A =/= B -> ph ) $=
      ( wne wceq wn df-ne con1i sylbi ) BCEBCFZGABCHAKDIJ $.
      $( [15-Feb-2007] $) $( [12-Feb-2007] $)
  $}

  ${
    necon1bi.1 $e |- ( A =/= B -> ph ) $.
    $( Contrapositive inference for inequality. $)
    necon1bi $p |- ( -. ph -> A = B ) $=
      ( wceq wn wne df-ne sylbir con1i ) BCEZAKFBCGABCHDIJ $.
      $( [23-Mar-2007] $) $( [18-Mar-2007] $)
  $}

  ${
    necon1i.1 $e |- ( A =/= B -> C = D ) $.
    $( Contrapositive inference for inequality. $)
    necon1i $p |- ( C =/= D -> A = B ) $=
      ( wceq wn wne df-ne sylbir necon1ai ) ABFZCDLGABHCDFABIEJK $.
      $( [19-Mar-2007] $) $( [18-Mar-2007] $)
  $}

  ${
    necon2ai.1 $e |- ( A = B -> -. ph ) $.
    $( Contrapositive inference for inequality. $)
    necon2ai $p |- ( ph -> A =/= B ) $=
      ( wceq wn wne con2i df-ne sylibr ) ABCEZFBCGKADHBCIJ $.
      $( [19-Jan-2007] $) $( [16-Jan-2007] $)
  $}

  ${
    necon2bi.1 $e |- ( ph -> A =/= B ) $.
    $( Contrapositive inference for inequality. $)
    necon2bi $p |- ( A = B -> -. ph ) $=
      ( wceq wne wn df-ne sylib con2i ) ABCEZABCFKGDBCHIJ $.
      $( [2-Apr-2007] $) $( [1-Apr-2007] $)
  $}

  ${
    necon2i.1 $e |- ( A = B -> C =/= D ) $.
    $( Contrapositive inference for inequality. $)
    necon2i $p |- ( C = D -> A =/= B ) $=
      ( wceq wne wn df-ne sylib necon2ai ) CDFZABABFCDGLHECDIJK $.
      $( [25-Apr-2007] $) $( [18-Mar-2007] $)
  $}

  ${
    necon2ad.1 $e |- ( ph -> ( A = B -> -. ps ) ) $.
    $( Contrapositive inference for inequality. $)
    necon2ad $p |- ( ph -> ( ps -> A =/= B ) ) $=
      ( wceq wn wne con2d df-ne syl6ibr ) ABCDFZGCDHALBEICDJK $.
      $( [26-Apr-2007] $) $( [19-Apr-2007] $)
  $}

  ${
    necon2bd.1 $e |- ( ph -> ( ps -> A =/= B ) ) $.
    $( Contrapositive inference for inequality. $)
    necon2bd $p |- ( ph -> ( A = B -> -. ps ) ) $=
      ( wceq wne wn df-ne syl6ib con2d ) ABCDFZABCDGLHECDIJK $.
      $( [14-Apr-2007] $) $( [13-Apr-2007] $)
  $}

  ${
    necon2d.1 $e |- ( ph -> ( A = B -> C =/= D ) ) $.
    $( Contrapositive inference for inequality. $)
    necon2d $p |- ( ph -> ( C = D -> A =/= B ) ) $=
      ( wceq wne wn df-ne syl6ib necon2ad ) ADEGZBCABCGDEHMIFDEJKL $.
      $( [18-Jan-2009] $) $( [28-Dec-2008] $)
  $}

  ${
    necon1abii.1 $e |- ( -. ph <-> A = B ) $.
    $( Contrapositive inference for inequality. $)
    necon1abii $p |- ( A =/= B <-> ph ) $=
      ( wne wceq wn df-ne con1bii bitri ) BCEBCFZGABCHAKDIJ $.
      $( [2-Jun-2007] $) $( [17-Mar-2007] $)
  $}

  ${
    necon1bbii.1 $e |- ( A =/= B <-> ph ) $.
    $( Contrapositive inference for inequality. $)
    necon1bbii $p |- ( -. ph <-> A = B ) $=
      ( wceq wn wne df-ne bitr3i con1bii ) BCEZAKFBCGABCHDIJ $.
      $( [20-Mar-2007] $) $( [17-Mar-2007] $)
  $}

  ${
    necon1abid.1 $e |- ( ph -> ( -. ps <-> A = B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon1abid $p |- ( ph -> ( A =/= B <-> ps ) ) $=
      ( wceq wn wne con1bid df-ne syl5bb ) ACDFZGBCDHABLEICDJK $.
      $( [22-Aug-2007] $) $( [21-Aug-2007] $)
  $}

  ${
    necon1bbid.1 $e |-  ( ph -> ( A =/= B <-> ps ) ) $.
    $( Contrapositive inference for inequality. $)
    necon1bbid $p |-  ( ph -> ( -. ps <-> A = B ) ) $=
      ( wceq wne wn df-ne syl5bbr con1bid ) ACDFZBACDGBLHECDIJK $.
      $( [15-Apr-2008] $) $( [31-Jan-2008] $)
  $}

  ${
    necon2abii.1 $e |- ( A = B <-> -. ph ) $.
    $( Contrapositive inference for inequality. $)
    necon2abii $p |- ( ph <-> A =/= B ) $=
      ( wne wceq wn bicomi necon1abii ) BCEAABCBCFAGDHIH $.
      $( [2-Mar-2007] $) $( [2-Mar-2007] $)
  $}

  ${
    necon2bbii.1 $e |- ( ph <-> A =/= B ) $.
    $( Contrapositive inference for inequality. $)
    necon2bbii $p |- ( A = B <-> -. ph ) $=
      ( wn wceq wne bicomi necon1bbii ) AEBCFABCABCGDHIH $.
      $( [19-Apr-2007] $) $( [13-Apr-2007] $)
  $}

  ${
    necon2abid.1 $e |- ( ph -> ( A = B <-> -. ps ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon2abid $p |- ( ph -> ( ps <-> A =/= B ) ) $=
      ( wceq wn wne con2bid df-ne syl6bbr ) ABCDFZGCDHALBEICDJK $.
      $( [18-Jul-2007] $) $( [18-Jul-2007] $)
  $}

  ${
    necon2bbid.1 $e |- ( ph -> ( ps <-> A =/= B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon2bbid $p |- ( ph -> ( A = B <-> -. ps ) ) $=
      ( wceq wne wn df-ne syl6bb con2bid ) ABCDFZABCDGLHECDIJK $.
      $( [17-Apr-2007] $) $( [13-Apr-2007] $)
  $}

  ${
    necon4ai.1 $e |- ( A =/= B -> -. ph ) $.
    $( Contrapositive inference for inequality. $)
    necon4ai $p |- ( ph -> A = B ) $=
      ( wceq wn wne df-ne sylbir con4i ) BCEZAKFBCGAFBCHDIJ $.
      $( [20-Jan-2007] $) $( [16-Jan-2007] $)
  $}

  ${
    necon4i.1 $e |- ( A =/= B -> C =/= D ) $.
    $( Contrapositive inference for inequality. $)
    necon4i $p |- ( C = D -> A = B ) $=
      ( wceq wne wn df-ne sylib necon4ai ) CDFZABABGCDGLHECDIJK $.
      $( [25-Mar-2007] $) $( [17-Mar-2007] $)
  $}

  ${
    necon4ad.1 $e |- ( ph -> ( A =/= B -> -. ps ) ) $.
    $( Contrapositive inference for inequality. $)
    necon4ad $p |- ( ph -> ( ps -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr con4d ) ACDFZBACDGBHLHECDIJK $.
      $( [15-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    necon4bd.1 $e |- ( ph -> ( -. ps -> A =/= B ) ) $.
    $( Contrapositive inference for inequality. $)
    necon4bd $p |- ( ph -> ( A = B -> ps ) ) $=
      ( wceq wn wne df-ne syl6ib con4d ) ABCDFZABGCDHLGECDIJK $.
      $( [3-Jun-2007] $) $( [1-Jun-2007] $)
  $}

  ${
    necon4d.1 $e |- ( ph -> ( A =/= B -> C =/= D ) ) $.
    $( Contrapositive inference for inequality. $)
    necon4d $p |- ( ph -> ( C = D -> A = B ) ) $=
      ( wceq wne wn df-ne syl6ib necon4ad ) ADEGZBCABCHDEHMIFDEJKL $.
      $( [15-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    necon4abid.1 $e |- ( ph -> ( A =/= B <-> -. ps ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon4abid $p |- ( ph -> ( A = B <-> ps ) ) $=
      ( wceq wne wn df-ne syl5bbr con4bid ) ACDFZBACDGBHLHECDIJK $.
      $( [12-Feb-2008] $) $( [11-Jan-2008] $)
  $}

  ${
    necon4bid.1 $e |- ( ph -> ( A =/= B <-> C =/= D ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon4bid $p |- ( ph -> ( A = B <-> C = D ) ) $=
      ( wceq wne wn necon2bbid nne syl6rbb ) ADEGBCHZIBCGAMDEFJBCKL $.
      $( [29-Jun-2007] $) $( [29-Jun-2007] $)
  $}

  ${
    necon1ad.1 $e |- ( ph -> ( -. ps -> A = B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon1ad $p |- ( ph -> ( A =/= B -> ps ) ) $=
      ( wceq wn wne con1d df-ne syl5ib ) ACDFZGBCDHABLEICDJK $.
      $( [17-Apr-2007] $) $( [2-Apr-2007] $)
  $}

  ${
    necon1bd.1 $e |- ( ph -> ( A =/= B -> ps ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon1bd $p |- ( ph -> ( -. ps -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr con1d ) ACDFZBACDGBLHECDIJK $.
      $( [25-Mar-2007] $) $( [21-Mar-2007] $)
  $}

  ${
    necon1d.1 $e |- ( ph -> ( A =/= B -> C = D ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon1d $p |- ( ph -> ( C =/= D -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr necon1ad ) ABCGZDEABCHDEGMIFBCJKL $.
      $( [29-Dec-2008] $) $( [28-Dec-2008] $)
  $}

  $( Contraposition law for inequality. $)
  nebi $p |- ( ( A = B <-> C = D ) <-> ( A =/= B <-> C =/= D ) ) $=
    ( wceq wb wne id necon3bid necon4bid impbii ) ABECDEFZABGCDGFZLABCDLHIMABCD
    MHJK $.
    $( [13-Jan-2009] $) $( [28-Dec-2008] $)

  ${
    pm2.61ne.1 $e |- ( A = B -> ( ps <-> ch ) ) $.
    pm2.61ne.2 $e |- ( ( ph /\ A =/= B ) -> ps ) $.
    pm2.61ne.3 $e |- ( ph -> ch ) $.
    $( Deduction eliminating an inequality in an antecedent. $)
    pm2.61ne $p |- ( ph -> ps ) $=
      ( wceq syl5bir impcom wne wn df-ne sylan2br pm2.61dan ) ADEIZBQABQBCAFHJK
      ADELBQMGDENOP $.
      $( [1-Jun-2006] $) $( [24-May-2006] $)
  $}

  ${
    pm2.61ine.1 $e |- ( A = B -> ph ) $.
    pm2.61ine.2 $e |- ( A =/= B -> ph ) $.
    $( Inference eliminating an inequality in an antecedent. $)
    pm2.61ine $p |- ph $=
      ( wceq wn wne df-ne sylbir pm2.61i ) BCFZADLGBCHABCIEJK $.
      $( [16-Jan-2007] $) $( [16-Jan-2007] $)
  $}

  ${
    pm2.61dne.1 $e |- ( ph -> ( A = B -> ps ) ) $.
    pm2.61dne.2 $e |- ( ph -> ( A =/= B -> ps ) ) $.
    $( Deduction eliminating an inequality in an antecedent. $)
    pm2.61dne $p |- ( ph -> ps ) $=
      ( wceq wne wn df-ne syl5ibr pm2.61d ) ACDGZBEACDHBMIFCDJKL $.
      $( [1-Jun-2007] $) $( [1-Jun-2007] $)
  $}

  $( Commutation of inequality. $)
  necom $p |- ( A =/= B <-> B =/= A ) $=
    ( eqcom necon3bii ) ABBAABCD $.
    $( [14-May-1999] $)

  ${
    necomd.1 $e |- ( ph -> A =/= B ) $.
    $( Deduction from commutative law for inequality. $)
    necomd $p |- ( ph -> B =/= A ) $=
      ( wne necom sylib ) ABCECBEDBCFG $.
      $( [13-Feb-2008] $) $( [12-Feb-2008] $)
  $}

  $( Logical OR with an equality. $)
  neor $p |- ( ( A = B \/ ps ) <-> ( A =/= B -> ps ) ) $=
    ( wceq wo wn wi wne df-or df-ne imbi1i bitr4i ) BCDZAEMFZAGBCHZAGMAIONABCJK
    L $.
    $( [30-Apr-2007] $) $( [29-Apr-2007] $)

  $( A DeMorgan's law for inequality. $)
  neanior $p |- ( ( A =/= B /\ C =/= D ) <-> -. ( A = B \/ C = D ) ) $=
    ( wne wa wceq wn wo df-ne anbi12i pm4.56 bitri ) ABEZCDEZFABGZHZCDGZHZFPRIH
    NQOSABJCDJKPRLM $.
    $( [20-May-2007] $) $( [18-May-2007] $)

  $( A DeMorgan's law for inequality. $)
  neorian $p |- ( ( A =/= B \/ C =/= D ) <-> -. ( A = B /\ C = D ) ) $=
    ( wne wo wceq wn wa df-ne orbi12i ianor bitr4i ) ABEZCDEZFABGZHZCDGZHZFPRIH
    NQOSABJCDJKPRLM $.
    $( [20-May-2007] $) $( [18-May-2007] $)

  ${
    nemtbir.1 $e |- A =/= B $.
    nemtbir.2 $e |- ( ph <-> A = B ) $.
    $( An inference from an inequality, related to modus tollens. $)
    nemtbir $p |- -. ph $=
      ( wceq wne wn df-ne mpbi mtbir ) ABCFZBCGLHDBCIJEK $.
      $( [13-Apr-2007] $) $( [13-Apr-2007] $)
  $}

  $( Equality theorem for negated membership. $)
  neleq1 $p |- ( A = B -> ( A e/ C <-> B e/ C ) ) $=
    ( wceq wcel wn wnel eleq1 notbid df-nel 3bitr4g ) ABDZACEZFBCEZFACGBCGLMNAB
    CHIACJBCJK $.
    $( [20-Nov-1994] $)

  $( Equality theorem for negated membership. $)
  neleq2 $p |- ( A = B -> ( C e/ A <-> C e/ B ) ) $=
    ( wceq wcel wn wnel eleq2 notbid df-nel 3bitr4g ) ABDZCAEZFCBEZFCAGCBGLMNAB
    CHICAJCBJK $.
    $( [20-Nov-1994] $)

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbne.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbne.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for inequality. $)
    hbne $p |- ( A =/= B -> A. x A =/= B ) $=
      ( wceq wn wal wne hbeq hbn df-ne albii 3imtr4i ) CDGZHZQAICDJZRAIPAABBCDE
      FKLCDMZRQASNO $.
      $( [4-Apr-2008] $) $( [10-Nov-2007] $)
  $}


