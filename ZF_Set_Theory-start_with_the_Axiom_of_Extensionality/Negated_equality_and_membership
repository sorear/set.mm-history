$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Negated equality and membership
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new connectives. $)
  $c =/= $. $( Not equal to (equal sign with slash through it). $)
  $c e/ $. $( Not an element of (epsilon with slash through it). $)

  $( Extend wff notation to include inequality. $)
  wne $a wff A =/= B $.

  $( Extend wff notation to include negated membership. $)
  wnel $a wff A e/ B $.

  $( Define inequality. $)
  df-ne $a |- ( A =/= B <-> -. A = B ) $.

  $( Define negated membership. $)
  df-nel $a |- ( A e/ B <-> -. A e. B ) $.

  $( Negation of inequality. $)
  nne $p |- ( -. A =/= B <-> A = B ) $=
    ( wceq wne wn df-ne con2bii bicomi ) ABCZABDZEJIABFGH $.
    $( [9-Jun-2006] $)

  $( Excluded middle with equality and inequality. $)
  exmidne $p |- ( A = B \/ A =/= B ) $=
    ( wceq wne wo wn exmid df-ne orbi2i mpbir ) ABCZABDZEKKFZEKGLMKABHIJ $.
    $( [3-Feb-2012] $)

  $( Law of noncontradiction with equality and inequality. $)
  nonconne $p |- -. ( A = B /\ A =/= B ) $=
    ( wceq wne wa wn pm3.24 df-ne anbi2i mtbir ) ABCZABDZEKKFZEKGLMKABHIJ $.
    $( [3-Feb-2012] $)

  $( Equality theorem for inequality. $)
  neeq1 $p |- ( A = B -> ( A =/= C <-> B =/= C ) ) $=
    ( wceq wn wne eqeq1 notbid df-ne 3bitr4g ) ABDZACDZEBCDZEACFBCFKLMABCGHACIB
    CIJ $.
    $( [19-Nov-1994] $)

  $( Equality theorem for inequality. $)
  neeq2 $p |- ( A = B -> ( C =/= A <-> C =/= B ) ) $=
    ( wceq wn wne eqeq2 notbid df-ne 3bitr4g ) ABDZCADZECBDZECAFCBFKLMABCGHCAIC
    BIJ $.
    $( [19-Nov-1994] $)

  ${
    neeq1i.1 $e |- A = B $.
    $( Inference for inequality. $)
    neeq1i $p |- ( A =/= C <-> B =/= C ) $=
      ( wceq wne wb neeq1 ax-mp ) ABEACFBCFGDABCHI $.
      $( [29-Apr-2005] $)

    $( Inference for inequality. $)
    neeq2i $p |- ( C =/= A <-> C =/= B ) $=
      ( wceq wne wb neeq2 ax-mp ) ABECAFCBFGDABCHI $.
      $( [29-Apr-2005] $)
  $}

  ${
    neeq1d.1 $e |- ( ph -> A = B ) $.
    $( Deduction for inequality. $)
    neeq1d $p |- ( ph -> ( A =/= C <-> B =/= C ) ) $=
      ( wceq wne wb neeq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $( [25-Oct-1999] $)

    $( Deduction for inequality. $)
    neeq2d $p |- ( ph -> ( C =/= A <-> C =/= B ) ) $=
      ( wceq wne wb neeq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [25-Oct-1999] $)
  $}

  ${
    necon3abii.1 $e |- ( A = B <-> ph ) $.
    $( Deduction from equality to inequality. $)
    necon3abii $p |- ( A =/= B <-> -. ph ) $=
      ( wne wceq wn df-ne notbii bitri ) BCEBCFZGAGBCHKADIJ $.
      $( [9-Nov-2007] $)
  $}

  ${
    necon3bbii.1 $e |- ( ph <-> A = B ) $.
    $( Deduction from equality to inequality. $)
    necon3bbii $p |- ( -. ph <-> A =/= B ) $=
      ( wne wn wceq bicomi necon3abii ) BCEAFABCABCGDHIH $.
      $( [13-Apr-2007] $)
  $}

  ${
    necon3bii.1 $e |- ( A = B <-> C = D ) $.
    $( Inference from equality to inequality. $)
    necon3bii $p |- ( A =/= B <-> C =/= D ) $=
      ( wne wceq wn necon3abii df-ne bitr4i ) ABFCDGZHCDFLABEICDJK $.
      $( [23-Feb-2005] $)
  $}

  ${
    necon3abid.1 $e |- ( ph -> ( A = B <-> ps ) ) $.
    $( Deduction from equality to inequality. $)
    necon3abid $p |- ( ph -> ( A =/= B <-> -. ps ) ) $=
      ( wceq wn wne notbid df-ne syl5bb ) ACDFZGBGCDHALBEICDJK $.
      $( [21-Mar-2007] $)
  $}

  ${
    necon3bbid.1 $e |- ( ph -> ( ps <-> A = B ) ) $.
    $( Deduction from equality to inequality. $)
    necon3bbid $p |- ( ph -> ( -. ps <-> A =/= B ) ) $=
      ( wne wn wceq bicomd necon3abid ) ACDFBGABCDABCDHEIJI $.
      $( [2-Jun-2007] $)
  $}

  ${
    necon3bid.1 $e |- ( ph -> ( A = B <-> C = D ) ) $.
    $( Deduction from equality to inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon3bid $p |- ( ph -> ( A =/= B <-> C =/= D ) ) $=
      ( wceq wn wne necon3bbid df-ne syl5bb ) ABCGZHDEIBCIAMDEFJBCKL $.
      $( [25-May-2011] $) $( [23-Feb-2005] $)

    $( Deduction from equality to inequality. $)
    necon3bidOLD $p |- ( ph -> ( A =/= B <-> C =/= D ) ) $=
      ( wne wceq wn necon3abid df-ne syl6bbr ) ABCGDEHZIDEGAMBCFJDEKL $.
      $( [23-Feb-2005] $)
  $}

  ${
    necon3ad.1 $e |- ( ph -> ( ps -> A = B ) ) $.
    $( Contrapositive law deduction for inequality.  (The proof was shortened
       by Andrew Salmon, 25-May-2011.) $)
    necon3ad $p |- ( ph -> ( A =/= B -> -. ps ) ) $=
      ( wne wceq wn nne syl6ibr con2d ) ABCDFZABCDGLHECDIJK $.
      $( [25-May-2011] $) $( [2-Apr-2007] $)

    $( Contrapositive law deduction for inequality. $)
    necon3adOLD $p |- ( ph -> ( A =/= B -> -. ps ) ) $=
      ( wceq wn wne con3d df-ne syl5ib ) ACDFZGBGCDHABLEICDJK $.
      $( [2-Apr-2007] $)
  $}

  ${
    necon3bd.1 $e |- ( ph -> ( A = B -> ps ) ) $.
    $( Contrapositive law deduction for inequality.  (The proof was shortened
       by Andrew Salmon, 25-May-2011.) $)
    necon3bd $p |- ( ph -> ( -. ps -> A =/= B ) ) $=
      ( wne wceq wn nne syl5ib con1d ) ACDFZBACDGBLHECDIJK $.
      $( [25-May-2011] $) $( [2-Apr-2007] $)

    $( Contrapositive law deduction for inequality. $)
    necon3bdOLD $p |- ( ph -> ( -. ps -> A =/= B ) ) $=
      ( wn wceq wne con3d df-ne syl6ibr ) ABFCDGZFCDHALBEICDJK $.
      $( [2-Apr-2007] $)
  $}

  ${
    necon3d.1 $e |- ( ph -> ( A = B -> C = D ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon3d $p |- ( ph -> ( C =/= D -> A =/= B ) ) $=
      ( wne wceq wn necon3ad df-ne syl6ibr ) ADEGBCHZIBCGAMDEFJBCKL $.
      $( [10-Jun-2006] $)
  $}

  ${
    necon3i.1 $e |- ( A = B -> C = D ) $.
    $( Contrapositive inference for inequality. $)
    necon3i $p |- ( C =/= D -> A =/= B ) $=
      ( wceq wi wne id necon3d ax-mp ) ABFCDFGZCDHABHGELABCDLIJK $.
      $( [9-Aug-2006] $)
  $}

  ${
    necon3ai.1 $e |- ( ph -> A = B ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon3ai $p |- ( A =/= B -> -. ph ) $=
      ( wne wceq wn nne sylibr con2i ) ABCEZABCFKGDBCHIJ $.
      $( [25-May-2011] $) $( [23-May-2007] $)

    $( Contrapositive inference for inequality. $)
    necon3aiOLD $p |- ( A =/= B -> -. ph ) $=
      ( wne wceq wn df-ne con3i sylbi ) BCEBCFZGAGBCHAKDIJ $.
      $( [23-May-2007] $)
  $}

  ${
    necon3bi.1 $e |- ( A = B -> ph ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon3bi $p |- ( -. ph -> A =/= B ) $=
      ( wne wn wceq nne sylbi con1i ) BCEZAKFBCGABCHDIJ $.
      $( [25-May-2011] $) $( [1-Jun-2007] $)

    $( Contrapositive inference for inequality. $)
    necon3biOLD $p |- ( -. ph -> A =/= B ) $=
      ( wn wceq wne con3i df-ne sylibr ) AEBCFZEBCGKADHBCIJ $.
      $( [1-Jun-2007] $)
  $}

  ${
    necon1ai.1 $e |- ( -. ph -> A = B ) $.
    $( Contrapositive inference for inequality. $)
    necon1ai $p |- ( A =/= B -> ph ) $=
      ( wne wceq wn df-ne con1i sylbi ) BCEBCFZGABCHAKDIJ $.
      $( [12-Feb-2007] $)
  $}

  ${
    necon1bi.1 $e |- ( A =/= B -> ph ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon1bi $p |- ( -. ph -> A = B ) $=
      ( wn wne wceq con3i nne sylib ) AEBCFZEBCGKADHBCIJ $.
      $( [25-May-2011] $) $( [18-Mar-2007] $)

    $( Contrapositive inference for inequality. $)
    necon1biOLD $p |- ( -. ph -> A = B ) $=
      ( wceq wn wne df-ne sylbir con1i ) BCEZAKFBCGABCHDIJ $.
      $( [18-Mar-2007] $)
  $}

  ${
    necon1i.1 $e |- ( A =/= B -> C = D ) $.
    $( Contrapositive inference for inequality. $)
    necon1i $p |- ( C =/= D -> A = B ) $=
      ( wceq wn wne df-ne sylbir necon1ai ) ABFZCDLGABHCDFABIEJK $.
      $( [18-Mar-2007] $)
  $}

  ${
    necon2ai.1 $e |- ( A = B -> -. ph ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon2ai $p |- ( ph -> A =/= B ) $=
      ( wne wn wceq nne sylbi con4i ) BCEZAKFBCGAFBCHDIJ $.
      $( [25-May-2011] $) $( [16-Jan-2007] $)

    $( Contrapositive inference for inequality. $)
    necon2aiOLD $p |- ( ph -> A =/= B ) $=
      ( wceq wn wne con2i df-ne sylibr ) ABCEZFBCGKADHBCIJ $.
      $( [16-Jan-2007] $)
  $}

  ${
    necon2bi.1 $e |- ( ph -> A =/= B ) $.
    $( Contrapositive inference for inequality. $)
    necon2bi $p |- ( A = B -> -. ph ) $=
      ( wceq wne wn df-ne sylib con2i ) ABCEZABCFKGDBCHIJ $.
      $( [1-Apr-2007] $)
  $}

  ${
    necon2i.1 $e |- ( A = B -> C =/= D ) $.
    $( Contrapositive inference for inequality. $)
    necon2i $p |- ( C = D -> A =/= B ) $=
      ( wceq wne wn df-ne sylib necon2ai ) CDFZABABFCDGLHECDIJK $.
      $( [18-Mar-2007] $)
  $}

  ${
    necon2ad.1 $e |- ( ph -> ( A = B -> -. ps ) ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon2ad $p |- ( ph -> ( ps -> A =/= B ) ) $=
      ( wne wceq wn nne syl5ib con4d ) ACDFZBACDGBHLHECDIJK $.
      $( [25-May-2011] $) $( [19-Apr-2007] $)

    $( Contrapositive inference for inequality. $)
    necon2adOLD $p |- ( ph -> ( ps -> A =/= B ) ) $=
      ( wceq wn wne con2d df-ne syl6ibr ) ABCDFZGCDHALBEICDJK $.
      $( [19-Apr-2007] $)
  $}

  ${
    necon2bd.1 $e |- ( ph -> ( ps -> A =/= B ) ) $.
    $( Contrapositive inference for inequality. $)
    necon2bd $p |- ( ph -> ( A = B -> -. ps ) ) $=
      ( wceq wne wn df-ne syl6ib con2d ) ABCDFZABCDGLHECDIJK $.
      $( [13-Apr-2007] $)
  $}

  ${
    necon2d.1 $e |- ( ph -> ( A = B -> C =/= D ) ) $.
    $( Contrapositive inference for inequality. $)
    necon2d $p |- ( ph -> ( C = D -> A =/= B ) ) $=
      ( wceq wne wn df-ne syl6ib necon2ad ) ADEGZBCABCGDEHMIFDEJKL $.
      $( [28-Dec-2008] $)
  $}

  ${
    necon1abii.1 $e |- ( -. ph <-> A = B ) $.
    $( Contrapositive inference for inequality. $)
    necon1abii $p |- ( A =/= B <-> ph ) $=
      ( wne wceq wn df-ne con1bii bitri ) BCEBCFZGABCHAKDIJ $.
      $( [17-Mar-2007] $)
  $}

  ${
    necon1bbii.1 $e |- ( A =/= B <-> ph ) $.
    $( Contrapositive inference for inequality. $)
    necon1bbii $p |- ( -. ph <-> A = B ) $=
      ( wceq wn wne df-ne bitr3i con1bii ) BCEZAKFBCGABCHDIJ $.
      $( [17-Mar-2007] $)
  $}

  ${
    necon1abid.1 $e |- ( ph -> ( -. ps <-> A = B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon1abid $p |- ( ph -> ( A =/= B <-> ps ) ) $=
      ( wceq wn wne con1bid df-ne syl5bb ) ACDFZGBCDHABLEICDJK $.
      $( [21-Aug-2007] $)
  $}

  ${
    necon1bbid.1 $e |- ( ph -> ( A =/= B <-> ps ) ) $.
    $( Contrapositive inference for inequality. $)
    necon1bbid $p |- ( ph -> ( -. ps <-> A = B ) ) $=
      ( wceq wne wn df-ne syl5bbr con1bid ) ACDFZBACDGBLHECDIJK $.
      $( [31-Jan-2008] $)
  $}

  ${
    necon2abii.1 $e |- ( A = B <-> -. ph ) $.
    $( Contrapositive inference for inequality. $)
    necon2abii $p |- ( ph <-> A =/= B ) $=
      ( wne wceq wn bicomi necon1abii ) BCEAABCBCFAGDHIH $.
      $( [2-Mar-2007] $)
  $}

  ${
    necon2bbii.1 $e |- ( ph <-> A =/= B ) $.
    $( Contrapositive inference for inequality. $)
    necon2bbii $p |- ( A = B <-> -. ph ) $=
      ( wn wceq wne bicomi necon1bbii ) AEBCFABCABCGDHIH $.
      $( [13-Apr-2007] $)
  $}

  ${
    necon2abid.1 $e |- ( ph -> ( A = B <-> -. ps ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon2abid $p |- ( ph -> ( ps <-> A =/= B ) ) $=
      ( wceq wn wne con2bid df-ne syl6bbr ) ABCDFZGCDHALBEICDJK $.
      $( [18-Jul-2007] $)
  $}

  ${
    necon2bbid.1 $e |- ( ph -> ( ps <-> A =/= B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon2bbid $p |- ( ph -> ( A = B <-> -. ps ) ) $=
      ( wceq wne wn df-ne syl6bb con2bid ) ABCDFZABCDGLHECDIJK $.
      $( [13-Apr-2007] $)
  $}

  ${
    necon4ai.1 $e |- ( A =/= B -> -. ph ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon4ai $p |- ( ph -> A = B ) $=
      ( wne wn wceq con2i nne sylib ) ABCEZFBCGKADHBCIJ $.
      $( [25-May-2011] $) $( [16-Jan-2007] $)

    $( Contrapositive inference for inequality. $)
    necon4aiOLD $p |- ( ph -> A = B ) $=
      ( wceq wn wne df-ne sylbir con4i ) BCEZAKFBCGAFBCHDIJ $.
      $( [16-Jan-2007] $)
  $}

  ${
    necon4i.1 $e |- ( A =/= B -> C =/= D ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon4i $p |- ( C = D -> A = B ) $=
      ( wceq wne wn necon2bi nne sylib ) CDFABGZHABFLCDEIABJK $.
      $( [25-May-2011] $) $( [17-Mar-2007] $)

    $( Contrapositive inference for inequality. $)
    necon4iOLD $p |- ( C = D -> A = B ) $=
      ( wceq wne wn df-ne sylib necon4ai ) CDFZABABGCDGLHECDIJK $.
      $( [17-Mar-2007] $)
  $}

  ${
    necon4ad.1 $e |- ( ph -> ( A =/= B -> -. ps ) ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon4ad $p |- ( ph -> ( ps -> A = B ) ) $=
      ( wne wn wceq con2d nne syl6ib ) ABCDFZGCDHALBEICDJK $.
      $( [25-May-2011] $) $( [2-Apr-2007] $)

    $( Contrapositive inference for inequality. $)
    necon4adOLD $p |- ( ph -> ( ps -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr con4d ) ACDFZBACDGBHLHECDIJK $.
      $( [2-Apr-2007] $)
  $}

  ${
    necon4bd.1 $e |- ( ph -> ( -. ps -> A =/= B ) ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon4bd $p |- ( ph -> ( A = B -> ps ) ) $=
      ( wne wn wceq con1d nne syl5ibr ) ACDFZGBCDHABLEICDJK $.
      $( [25-May-2011] $) $( [1-Jun-2007] $)

    $( Contrapositive inference for inequality. $)
    necon4bdOLD $p |- ( ph -> ( A = B -> ps ) ) $=
      ( wceq wn wne df-ne syl6ib con4d ) ABCDFZABGCDHLGECDIJK $.
      $( [1-Jun-2007] $)
  $}

  ${
    necon4d.1 $e |- ( ph -> ( A =/= B -> C =/= D ) ) $.
    $( Contrapositive inference for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon4d $p |- ( ph -> ( C = D -> A = B ) ) $=
      ( wceq wne wn necon2bd nne syl6ib ) ADEGBCHZIBCGAMDEFJBCKL $.
      $( [25-May-2011] $) $( [2-Apr-2007] $)

    $( Contrapositive inference for inequality. $)
    necon4dOLD $p |- ( ph -> ( C = D -> A = B ) ) $=
      ( wceq wne wn df-ne syl6ib necon4ad ) ADEGZBCABCHDEHMIFDEJKL $.
      $( [2-Apr-2007] $)
  $}

  ${
    necon4abid.1 $e |- ( ph -> ( A =/= B <-> -. ps ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon4abid $p |- ( ph -> ( A = B <-> ps ) ) $=
      ( wceq wne wn df-ne syl5bbr con4bid ) ACDFZBACDGBHLHECDIJK $.
      $( [11-Jan-2008] $)
  $}

  ${
    necon4bid.1 $e |- ( ph -> ( A =/= B <-> C =/= D ) ) $.
    $( Contrapositive law deduction for inequality. $)
    necon4bid $p |- ( ph -> ( A = B <-> C = D ) ) $=
      ( wceq wne wn necon2bbid nne syl6rbb ) ADEGBCHZIBCGAMDEFJBCKL $.
      $( [29-Jun-2007] $)
  $}

  ${
    necon1ad.1 $e |- ( ph -> ( -. ps -> A = B ) ) $.
    $( Contrapositive deduction for inequality. $)
    necon1ad $p |- ( ph -> ( A =/= B -> ps ) ) $=
      ( wceq wn wne con1d df-ne syl5ib ) ACDFZGBCDHABLEICDJK $.
      $( [2-Apr-2007] $)
  $}

  ${
    necon1bd.1 $e |- ( ph -> ( A =/= B -> ps ) ) $.
    $( Contrapositive deduction for inequality.  (The proof was shortened by
       Andrew Salmon, 25-May-2011.) $)
    necon1bd $p |- ( ph -> ( -. ps -> A = B ) ) $=
      ( wn wne wceq con3d nne syl6ib ) ABFCDGZFCDHALBEICDJK $.
      $( [25-May-2011] $) $( [21-Mar-2007] $)

    $( Contrapositive deduction for inequality. $)
    necon1bdOLD $p |- ( ph -> ( -. ps -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr con1d ) ACDFZBACDGBLHECDIJK $.
      $( [21-Mar-2007] $)
  $}

  ${
    necon1d.1 $e |- ( ph -> ( A =/= B -> C = D ) ) $.
    $( Contrapositive law deduction for inequality.  (The proof was shortened
       by Andrew Salmon, 25-May-2011.) $)
    necon1d $p |- ( ph -> ( C =/= D -> A = B ) ) $=
      ( wne wceq wn nne syl6ibr necon4ad ) ADEGZBCABCGDEHMIFDEJKL $.
      $( [25-May-2011] $) $( [28-Dec-2008] $)

    $( Contrapositive law deduction for inequality. $)
    necon1dOLD $p |- ( ph -> ( C =/= D -> A = B ) ) $=
      ( wceq wne wn df-ne syl5ibr necon1ad ) ABCGZDEABCHDEGMIFBCJKL $.
      $( [28-Dec-2008] $)
  $}

  $( Contraposition law for inequality. $)
  nebi $p |- ( ( A = B <-> C = D ) <-> ( A =/= B <-> C =/= D ) ) $=
    ( wceq wb wne id necon3bid necon4bid impbii ) ABECDEFZABGCDGFZLABCDLHIMABCD
    MHJK $.
    $( [28-Dec-2008] $)

  $( Theorem *13.18 in [WhiteheadRussell] p. 178.  (Contributed by Andrew
     Salmon, 3-Jun-2011.) $)
  pm13.18 $p |- ( ( A = B /\ A =/= C ) -> B =/= C ) $=
    ( wceq wne eqeq1 biimprd necon3d imp ) ABDZACEBCEJBCACJACDBCDABCFGHI $.
    $( [6-Sep-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.181 in [WhiteheadRussell] p. 178.  (Contributed by Andrew
     Salmon, 3-Jun-2011.) $)
  pm13.181 $p |- ( ( A = B /\ B =/= C ) -> A =/= C ) $=
    ( wceq wne pm13.18 eqcom sylanb ) BADBCEACEABDBACFABGH $.
    $( [6-Sep-2011] $) $( [3-Jun-2011] $)

  ${
    pm2.61ne.1 $e |- ( A = B -> ( ps <-> ch ) ) $.
    pm2.61ne.2 $e |- ( ( ph /\ A =/= B ) -> ps ) $.
    pm2.61ne.3 $e |- ( ph -> ch ) $.
    $( Deduction eliminating an inequality in an antecedent.  (The proof was
       shortened by Andrew Salmon, 25-May-2011.) $)
    pm2.61ne $p |- ( ph -> ps ) $=
      ( wne wi expcom wn wceq nne syl5bir sylbi pm2.61i ) DEIZABJZARBGKRLDEMZSD
      ENTBCAFHOPQ $.
      $( [25-May-2011] $) $( [24-May-2006] $)

    $( Deduction eliminating an inequality in an antecedent. $)
    pm2.61neOLD $p |- ( ph -> ps ) $=
      ( wceq syl5bir impcom wne wn df-ne sylan2br pm2.61dan ) ADEIZBQABQBCAFHJK
      ADELBQMGDENOP $.
      $( [24-May-2006] $)
  $}

  ${
    pm2.61ine.1 $e |- ( A = B -> ph ) $.
    pm2.61ine.2 $e |- ( A =/= B -> ph ) $.
    $( Inference eliminating an inequality in an antecedent.  (The proof was
       shortened by Andrew Salmon, 25-May-2011.) $)
    pm2.61ine $p |- ph $=
      ( wne wn wceq nne sylbi pm2.61i ) BCFZAELGBCHABCIDJK $.
      $( [25-May-2011] $) $( [16-Jan-2007] $)

    $( Inference eliminating an inequality in an antecedent. $)
    pm2.61ineOLD $p |- ph $=
      ( wceq wn wne df-ne sylbir pm2.61i ) BCFZADLGBCHABCIEJK $.
      $( [16-Jan-2007] $)
  $}

  ${
    pm2.61dne.1 $e |- ( ph -> ( A = B -> ps ) ) $.
    pm2.61dne.2 $e |- ( ph -> ( A =/= B -> ps ) ) $.
    $( Deduction eliminating an inequality in an antecedent.  (The proof was
       shortened by Andrew Salmon, 25-May-2011.) $)
    pm2.61dne $p |- ( ph -> ps ) $=
      ( wne wceq wn nne syl5ib pm2.61d ) ACDGZBFACDHBMIECDJKL $.
      $( [25-May-2011] $) $( [1-Jun-2007] $)

    $( Deduction eliminating an inequality in an antecedent. $)
    pm2.61dneOLD $p |- ( ph -> ps ) $=
      ( wceq wne wn df-ne syl5ibr pm2.61d ) ACDGZBEACDHBMIFCDJKL $.
      $( [1-Jun-2007] $)
  $}

  ${
    pm2.61dane.1 $e |- ( ( ph /\ A = B ) -> ps ) $.
    pm2.61dane.2 $e |- ( ( ph /\ A =/= B ) -> ps ) $.
    $( Deduction eliminating an inequality in an antecedent. $)
    pm2.61dane $p |- ( ph -> ps ) $=
      ( wceq ex wne pm2.61dne ) ABCDACDGBEHACDIBFHJ $.
      $( [30-Nov-2011] $)
  $}

  $( Commutation of inequality. $)
  necom $p |- ( A =/= B <-> B =/= A ) $=
    ( eqcom necon3bii ) ABBAABCD $.
    $( [14-May-1999] $)

  ${
    necomd.1 $e |- ( ph -> A =/= B ) $.
    $( Deduction from commutative law for inequality. $)
    necomd $p |- ( ph -> B =/= A ) $=
      ( wne necom sylib ) ABCECBEDBCFG $.
      $( [12-Feb-2008] $)
  $}

  $( Logical OR with an equality. $)
  neor $p |- ( ( A = B \/ ps ) <-> ( A =/= B -> ps ) ) $=
    ( wceq wo wn wi wne df-or df-ne imbi1i bitr4i ) BCDZAEMFZAGBCHZAGMAIONABCJK
    L $.
    $( [29-Apr-2007] $)

  $( A DeMorgan's law for inequality. $)
  neanior $p |- ( ( A =/= B /\ C =/= D ) <-> -. ( A = B \/ C = D ) ) $=
    ( wne wa wceq wn wo df-ne anbi12i pm4.56 bitri ) ABEZCDEZFABGZHZCDGZHZFPRIH
    NQOSABJCDJKPRLM $.
    $( [18-May-2007] $)

  $( A DeMorgan's law for inequality. $)
  neorian $p |- ( ( A =/= B \/ C =/= D ) <-> -. ( A = B /\ C = D ) ) $=
    ( wne wo wceq wn wa df-ne orbi12i ianor bitr4i ) ABEZCDEZFABGZHZCDGZHZFPRIH
    NQOSABJCDJKPRLM $.
    $( [18-May-2007] $)

  ${
    nemtbir.1 $e |- A =/= B $.
    nemtbir.2 $e |- ( ph <-> A = B ) $.
    $( An inference from an inequality, related to modus tollens. $)
    nemtbir $p |- -. ph $=
      ( wceq wne wn df-ne mpbi mtbir ) ABCFZBCGLHDBCIJEK $.
      $( [13-Apr-2007] $)
  $}

  $( Two classes are different if they don't contain the same element. $)
  nelne $p |- ( ( A e. B /\ -. A e. C ) -> B =/= C ) $=
    ( wcel wn wne wceq eleq2 biimpcd necon3bd imp ) ABDZACDZEBCFLMBCBCGLMBCAHIJ
    K $.
    $( [3-Feb-2012] $)

  $( Equality theorem for negated membership. $)
  neleq1 $p |- ( A = B -> ( A e/ C <-> B e/ C ) ) $=
    ( wceq wcel wn wnel eleq1 notbid df-nel 3bitr4g ) ABDZACEZFBCEZFACGBCGLMNAB
    CHIACJBCJK $.
    $( [20-Nov-1994] $)

  $( Equality theorem for negated membership. $)
  neleq2 $p |- ( A = B -> ( C e/ A <-> C e/ B ) ) $=
    ( wceq wcel wn wnel eleq2 notbid df-nel 3bitr4g ) ABDZCAEZFCBEZFCAGCBGLMNAB
    CHICAJCBJK $.
    $( [20-Nov-1994] $)

  ${
    $d y A $.  $d y B $.
    hbne.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbne.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for inequality.  (Unnecessary distinct
       variable restrictions were removed by Andrew Salmon, 11-Jul-2011.) $)
    hbne $p |- ( A =/= B -> A. x A =/= B ) $=
      ( wceq wn wal wne hbeq hbn df-ne albii 3imtr4i ) CDGZHZQAICDJZRAIPAABBCDE
      FKLCDMZRQASNO $.
      $( [11-Jul-2011] $) $( [10-Nov-2007] $)
  $}

  ${
    $d y A $.  $d z B $.
    hbnel.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbnel.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( Bound-variable hypothesis builder for inequality.  (Contributed by by
       David Abernethy, 26-Jun-2011.)  (Unnecessary distinct variable
       restrictions were removed by Andrew Salmon, 11-Jul-2011.) $)
    hbnel $p |- ( A e/ B -> A. x A e/ B ) $=
      ( wcel wn wal wnel hbel hbn df-nel albii 3imtr4i ) DEHZIZRAJDEKZSAJQAABCD
      EFGLMDENZSRATOP $.
      $( [11-Jul-2011] $) $( [26-Jun-2011] $)
  $}


