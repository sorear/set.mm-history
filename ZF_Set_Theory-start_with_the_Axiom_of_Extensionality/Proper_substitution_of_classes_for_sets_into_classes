$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Proper substitution of classes for sets into classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c [_ $.  $( Underlined left bracket $)
  $c ]_ $.  $( Underlined right bracket $)

  $( Extend class notation to include the proper substitution of a class for a
     set into another class. $)
  csb $a class [_ A / x ]_ B $.

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Define the proper substitution of a class for a set into another class.
       The underlined brackets distinguish it from the substitution into a wff,
       ~ wsbc , to prevent ambiguity.  Theorem ~ sbcel1g shows an example of
       how ambiguity could arise if we didn't use distinguished brackets.
       Theorem ~ sbccsbg recreates substitution into a wff from this
       definition. $)
    df-csb $a |- [_ A / x ]_ B = { y | [ A / x ] y e. B } $.
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Analog of ~ dfsbcq for proper substitution into a class. $)
    csbeq1 $p |- ( A = B -> [_ A / x ]_ C = [_ B / x ]_ C ) $=
      ( vy wceq cv wcel wsbc cab csb dfsbcq abbidv df-csb 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [14-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.  $d z D $.
    cbvcsbv.1 $e |- ( x = y -> B = C ) $.
    $( Change the bound variable of a proper substitution into a class using
       implicit substitution. $)
    cbvcsbv $p |- ( A e. D -> [_ A / x ]_ B = [_ A / y ]_ C ) $=
      ( vz wcel cv wsbc cab csb wceq eleq2d cbvsbcv abbidv df-csb 3eqtr4g ) CFI
      ZHJZDIZACKZHLUAEIZBCKZHLACDMBCEMTUCUEHUBUDABCFAJBJNDEUAGOPQAHCDRBHCERS $.
      $( [6-Oct-2008] $) $( [30-Sep-2008] $)
  $}

  ${
    csbeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for proper substitution into a class. $)
    csbeq1d $p |- ( ph -> [_ A / x ]_ C = [_ B / x ]_ C ) $=
      ( wceq csb csbeq1 syl ) ACDGBCEHBDEHGFBCDEIJ $.
      $( [4-Dec-2005] $) $( [3-Dec-2005] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( Analog of ~ sbid for proper substitution into a class. $)
    csbid $p |- [_ x / x ]_ A = A $=
      ( vy cv csb wcel wsbc cab df-csb sbid abbii abid2 3eqtri ) AADZBECDBFZANG
      ZCHOCHBACNBIPOCOAJKCBLM $.
      $( [5-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Equality theorem for proper substitution into a class. $)
    csbeq1a $p |- ( x = A -> B = [_ A / x ]_ B ) $=
      ( cv wceq csb csbeq1 csbid syl5eqr ) ADZBEAJCFABCFCAJBCGACHI $.
      $( [23-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d z A $.  $d y z B $.  $d z C $.  $d x z $.
    $( Composition law for chained substitutions into a class. $)
    csbcog $p |- ( A e. C -> [_ A / y ]_ [_ y / x ]_ B = [_ A / x ]_ B ) $=
      ( vz wcel cv csb wsbc cab df-csb abeq2i sbcbii sbccog bitrd abbidv
      3eqtr4g ) CEGZFHZABHZDIZGZBCJZFKTDGZACJZFKBCUBIACDISUDUFFSUDUEAUAJZBCJUFU
      CUGBCEUGFUBAFUADLMNUEABCEOPQBFCUBLAFCDLR $.
      $( [15-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( The existence of proper substitution into a class. $)
    csbexg $p |- ( ( A e. C /\ A. x B e. D ) -> [_ A / x ]_ B e. V ) $=
      ( vy wcel wal wa cv wsbc cab cvv csb a4sbc elisset abid2 syl5eqel 19.20i
      syl5 imp wb ax-17 sbcabel adantr mpbid df-csb ) BDGZCEGZAHZIZFJZCGZABKFLZ
      MABCNUKUMFLZMGZABKZUNMGZUHUJUQUHUPAHUQUJUPABDOUIUPAUICMUOCEPFCQRSTUAUHUQU
      RUBUJUMAFFBMDULMGAUCUDUEUFAFBCUGR $.
      $( [28-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    csbex.1 $e |- A e. V $.
    csbex.2 $e |- B e. V $.
    $( The existence of proper substitution into a class. $)
    csbex $p |- [_ A / x ]_ B e. V $=
      ( cvv wcel wal csb ax-gen csbexg mp2an ) BFGCFGZAHABCIFGDMAEJABCFFKL $.
      $( [8-Aug-2007] $) $( [7-Aug-2007] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y C $.  $d x y $.
    csbconstgf.1 $e |- ( y e. B -> A. x y e. B ) $.
    $( Substitution doesn't affect a constant ` B ` (in which ` x ` is not
       free). $)
    csbconstgf $p |- ( A e. C -> [_ A / x ]_ B = B ) $=
      ( wcel cv wsbc cab csb sbcgf abbidv abid2 syl6eq df-csb syl5eq ) CEGZBHDG
      ZACIZBJZDACDKRUASBJDRTSBSACEFLMBDNOABCDPQ $.
      $( [16-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d x y z $.  $d y z A $.  $d y z B $.  $d y z C $.
    $( Distribute proper substitution through a membership relation. $)
    sbcel12g $p |- ( A e. D -> ( [ A / x ] B e. C <->
                   [_ A / x ]_ B e. [_ A / x ]_ C ) ) $=
      ( vy vz wcel wsbc cv cab csb cvv wb elisset wceq wa wex sbcexg df-clel
      sbcbii wal dfcleq sbcalg sbcbidig ax-17 sbcgf bibi1d bitrd albidv 3bitrd
      abeq2 syl6rbbr wi eleq1 sbcbidv expcom 19.21aiv visset elabgt mpan syl
      anbi12d sbcang bitr4d exbidv 3bitr4d syl6bbr df-csb eleq12i ) BEHZCDHZABI
      ZFJZCHZABIZFKZVNDHZABIZFKZHZABCLZABDLZHVKBMHZVMWANBEOWDVMGJZVQPZWEVTHZQZG
      RZWAWDWECPZWEDHZQZGRZABIWLABIZGRVMWIWLGABMSVLWMABMGCDTUAWDWHWNGWDWHWJABIZ
      WKABIZQWNWDWFWOWGWPWDWOVNWEHZVPNZFUBZWFWDWOWQVONZFUBZABIWTABIZFUBWSWJXAAB
      MFWECUCUAWTFABMUDWDXBWRFWDXBWQABIZVPNWRWQVOABMUEWDXCWQVPWQABMWQAUFUGUHUIU
      JUKVPFWEULUMWDVNWEPZVSWPNZUNZFUBZWGWPNZWDXFFXDWDXEXDVRWKABMVNWEDUOUPUQURW
      EMHXGXHGUSVSWPFWEMUTVAVBVCWJWKABMVDVEVFVGGVQVTTVHVBWBVQWCVTAFBCVIAFBDVIVJ
      VH $.
      $( [24-Nov-2005] $) $( [10-Nov-2005] $)

    $( Distribute proper substitution through an equality relation. $)
    sbceqdig $p |- ( A e. D -> ( [ A / x ] B = C <->
                   [_ A / x ]_ B = [_ A / x ]_ C ) ) $=
      ( vy vz wcel wceq wsbc cv cab csb cvv wb elisset wal sbcalg dfcleq
      sbcbii wi eleq1 sbcbidv expcom 19.21aiv visset elabgt mpan syl bibi12d
      sbcbidig bitr4d albidv 3bitr4d syl6bbr df-csb eqeq12i ) BEHZCDIZABJZFKZCH
      ZABJZFLZVADHZABJZFLZIZABCMZABDMZIURBNHZUTVHOBEPVKUTGKZVDHZVLVGHZOZGQZVHVK
      VLCHZVLDHZOZGQZABJVSABJZGQUTVPVSGABNRUSVTABNGCDSTVKVOWAGVKVOVQABJZVRABJZO
      WAVKVMWBVNWCVKVAVLIZVCWBOZUAZFQZVMWBOZVKWFFWDVKWEWDVBVQABNVAVLCUBUCUDUEVL
      NHZWGWHGUFZVCWBFVLNUGUHUIVKWDVFWCOZUAZFQZVNWCOZVKWLFWDVKWKWDVEVRABNVAVLDU
      BUCUDUEWIWMWNWJVFWCFVLNUGUHUIUJVQVRABNUKULUMUNGVDVGSUOUIVIVDVJVGAFBCUPAFB
      DUPUQUO $.
      $( [28-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x y C $.  $d y D $.
    $( Move proper substitution in and out of a membership relation.
       Note that the scope of ` [ A / x ] ` is the wff ` B e. C ` , whereas
       the scope of ` [_ A / x ]_ ` is the class ` B ` . $)
    sbcel1g $p |- ( A e. D -> ( [ A / x ] B e. C <->
                    [_ A / x ]_ B e. C ) ) $=
      ( vy wcel wsbc csb sbcel12g cv ax-17 csbconstgf eleq2d bitrd ) BEGZCDGABH
      ABCIZABDIZGQDGABCDEJPRDQAFBDEFKDGALMNO $.
      $( [29-Nov-2005] $) $( [10-Nov-2005] $)

    $( Move proper substitution to first argument of an equality. $)
    sbceq1dig $p |- ( A e. D -> ( [ A / x ] B = C <->
                    [_ A / x ]_ B = C ) ) $=
      ( vy wcel wceq wsbc csb sbceqdig cv ax-17 csbconstgf eqeq2d bitrd ) BEGZC
      DHABIABCJZABDJZHRDHABCDEKQSDRAFBDEFLDGAMNOP $.
      $( [5-Dec-2005] $) $( [30-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y D $.
    $( Move proper substitution in and out of a membership relation. $)
    sbcel2g $p |- ( A e. D -> ( [ A / x ] B e. C <->
                    B e. [_ A / x ]_ C ) ) $=
      ( vy wcel wsbc csb sbcel12g cv ax-17 csbconstgf eleq1d bitrd ) BEGZCDGABH
      ABCIZABDIZGCRGABCDEJPQCRAFBCEFKCGALMNO $.
      $( [28-Nov-2005] $) $( [14-Nov-2005] $)

    $( Move proper substitution to second argument of an equality. $)
    sbceq2dig $p |- ( A e. D -> ( [ A / x ] B = C <->
                    B = [_ A / x ]_ C ) ) $=
      ( vy wcel wceq wsbc csb sbceqdig cv ax-17 csbconstgf eqeq1d bitrd ) BEGZC
      DHABIABCJZABDJZHCSHABCDEKQRCSAFBCEFLCGAMNOP $.
      $( [11-Dec-2005] $) $( [30-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d x z B $.  $d z C $.  $d x y $.
    $( Commutative law for double substitution into a class. $)
    csbcomg $p |- ( ( A e. R /\ B e. S ) ->
                 [_ A / x ]_ [_ B / y ]_ C = [_ B / y ]_ [_ A / x ]_ C ) $=
      ( vz cvv wcel csb wceq wa cv wsbc sbccomg wb sbcel2g sbcbidv ancoms
      3bitr3d adantr adantl eqrdv elisset syl2an ) CIJZDIJZACBDEKZKZBDACEKZKZLC
      FJDGJUGUHMZHUJULUMHNZUIJZACOZUNUKJZBDOZUNUJJZUNULJZUMUNEJZBDOZACOZVAACOZB
      DOUPURVAABCDIIPUHUGVCUPQUHVBUOACIBDUNEIRSTUGVDUQBDIACUNEIRSUAUGUPUSQUHACU
      NUIIRUBUHURUTQUGBDUNUKIRUCUAUDCFUEDGUEUF $.
      $( [28-Nov-2005] $) $( [14-Nov-2005] $)
  $}

  ${
    csbeq2d.1 $e |- ( ph -> A. x ph ) $.
    csbeq2d.2 $e |- ( ph -> B = C ) $.
    $( Formula-building deduction rule for class substitution. $)
    csbeq2d $p |- ( ( ph /\ A e. D ) -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( wcel csb wceq wsbc wal a4sbc 19.21ai syl5 sbceqdig sylibd impcom ) CFIZ
      ABCDJBCEJKZTADEKZBCLZUATUBBMUCAUBBCFNAUBBGHOPBCDEFQRS $.
      $( [29-Nov-2005] $) $( [22-Nov-2005] $)
  $}

  ${
    $d x ph $.
    csbeq2dv.1 $e |- ( ph -> B = C ) $.
    $( Formula-building deduction rule for class substitution. $)
    csbeq2dv $p |- ( ( ph /\ A e. D ) -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( ax-17 csbeq2d ) ABCDEFABHGI $.
      $( [30-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    csbeq2i.1 $e |- B = C $.
    $( Formula-building inference rule for class substitution. $)
    csbeq2i $p |- ( A e. D -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( wcel wceq wsbc csb sbcth sbceqdig mpbid ) BEGCDHZABIABCJABDJHNABEFKABCD
      ELM $.
      $( [1-Dec-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d x y z $.
    $( The proper substitution of a class for set variable results in the class
       (if the class exists). $)
    csbvarg $p |- ( A e. B -> [_ A / x ]_ x = A ) $=
      ( vy vz wcel cvv cv csb wceq elisset visset wsbc cab sbcel2gv abbi1dv
      df-csb syl5eq ax-mp csbeq2i csbcog 3eqtr3d syl ) BCFBGFZABAHZIZBJBCKUDDBA
      DHZUEIZIDBUGIZUFBDBUHUGGUGGFZUHUGJDLUJEHZUEFAUGMZENUGUHUJULEUGAUKUGGOPAEU
      GUEQRSTADBUEGUAUDUKUGFDBMZENBUIUDUMEBDUKBGOPDEBUGQRUBUC $.
      $( [1-Dec-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d x y $.
    $( Substitution into a wff expressed in terms of substitution into a
       class. $)
    sbccsbg $p |- ( A e. B ->
                 ( [ A / x ] ph <-> y e. [_ A / x ]_ { y | ph } ) ) $=
      ( wcel cv cab wsbc csb abid sbcbii sbcel2g bitr3d ) DEFCGZACHZFZBDIABDIOB
      DPJFQABDEACKLBDOPEMN $.
      $( [15-Aug-2007] $) $( [15-Aug-2007] $)
  $}

  $( Substitution into a wff expressed in using substitution into a class. $)
  sbccsb2g $p |- ( A e. B ->
               ( [ A / x ] ph <-> A e. [_ A / x ]_ { x | ph } ) ) $=
    ( wcel cv cab wsbc csb sbcel12g abid sbcbii csbvarg eleq1d 3bitr3d ) CDEZBF
    ZABGZEZBCHBCQIZBCRIZEABCHCUAEBCQRDJSABCDABKLPTCUABCDMNO $.
    $( [1-Dec-2005] $) $( [27-Nov-2005] $)

  ${
    $d y z A $.  $d z B $.  $d x y z $.
    hbcsb1g.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsb1g $p |- ( A e. C ->
                 ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) ) $=
      ( vz wcel cvv cv csb wal wi elisset wsbc cab ax-17 hbel 19.21ai hbsbc1g
      hbabd df-csb eleq2i albii 3imtr4g syl ) CEHCIHZBJZACDKZHZUJALZMCENUGUHGJD
      HZACOZGPZHZUOALUJUKUGUMAGBUGUGGLAABBCIFUHIHAQRUGGQSULABCIFTUAUIUNUHAGCDUB
      UCZUJUOAUPUDUEUF $.
      $( [29-Nov-2005] $) $( [11-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d z B $.  $d x y z $.
    hbcsb1.1 $e |- A e. V $.
    hbcsb1.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsb1 $p |- ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) $=
      ( cvv wcel cv csb wal wi hbcsb1g ax-mp ) CGHBIACDJHZOAKLEABCDGFMN $.
      $( [17-Aug-2006] $) $( [17-Aug-2006] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x z $.  $d w y $.
    hbcsbg.1 $e |- ( z e. A -> A. x z e. A ) $.
    hbcsbg.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsbg $p |- ( A e. C ->
                 ( z e. [_ A / y ]_ B -> A. x z e. [_ A / y ]_ B ) ) $=
      ( vw wcel cvv cv csb wal wi elisset wsbc cab ax-17 hbel 19.21ai hbsbcg
      hbabd df-csb eleq2i albii 3imtr4g syl ) DFJDKJZCLZBDEMZJZULANZODFPUIUJILZ
      EJZBDQZIRZJZURANULUMUIUPAICUIUIINAACCDKGUJKJASTUIISUAUOABCDKGACCUNEUJUNJA
      SHTUBUCUKUQUJBIDEUDUEZULURAUSUFUGUH $.
      $( [7-Dec-2005] $) $( [20-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d z B $.  $d y z ph $.  $d x y z $.
    hbcsb1gd.1 $e |- ( ph -> A. x ph ) $.
    hbcsb1gd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of ~ hbcsb1g . $)
    hbcsb1gd $p |- ( ( ph /\ A e. C ) ->
                 ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) ) $=
      ( vz cvv wcel cv csb wal wi wa a1d ax-17 a1i hbeld hband anabsi5 wsbc
      hbsbc1gd wb sbcel2g adantl albid 3imtr3d elisset sylan2 ) ADJKZCLZBDEMZKZ
      UOBNODFKAULPZBIUMUNAULUPBNAAULBAABNAGQABCDJGHUMJKZUQBNOAUQBRSTUAUBZILZUMK
      ZUTBNOUPUTBRSUPUSEKZBDUCZVBBNUSUNKZVCBNAVABCDJGHUDULVBVCUEABDUSEJUFUGZUPV
      BVCBURVDUHUITDFUJUK $.
      $( [24-May-2006] $) $( [23-May-2006] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w z ph $.  $d w x z $.  $d w y $.
    hbcsbgd.1 $e |- ( ph -> A. x ph ) $.
    hbcsbgd.2 $e |- ( ph -> A. y ph ) $.
    hbcsbgd.3 $e |- ( ph -> ( z e. A -> A. x z e. A ) ) $.
    hbcsbgd.4 $e |- ( ph -> ( z e. B -> A. x z e. B ) ) $.
    $( Deduction version of ~ hbcsbg . $)
    hbcsbgd $p |- ( ( ph /\ A e. C ) ->
                 ( z e. [_ A / y ]_ B -> A. x z e. [_ A / y ]_ B ) ) $=
      ( vw cvv wcel cv csb wal wi wa a1d ax-17 a1i hbeld hband anabsi5 wsbc
      hbsbcgd wb sbcel2g adantl albid 3imtr3d elisset sylan2 ) AEMNZDOZCEFPZNZU
      RBQREGNAUOSZBLUPUQAUOUSBQAAUOBAABQAHTABDEMHJUPMNZUTBQRAUTBUAUBUCUDUEZLOZU
      PNZVCBQRUSVCBUAUBUSVBFNZCEUFZVEBQVBUQNZVFBQAVDBCDEMHIJABDVBFHUPVBNZVGBQRA
      VGBUAUBKUCUGUOVEVFUHACEVBFMUIUJZUSVEVFBVAVHUKULUCEGUMUN $.
      $( [30-Nov-2005] $) $( [21-Nov-2005] $)
  $}

  ${
    $d z A $.  $d w B $.  $d z C $.  $d w x y z $.
    csbhypf.1 $e |- ( z e. A -> A. x z e. A ) $.
    csbhypf.2 $e |- ( z e. C -> A. x z e. C ) $.
    csbhypf.3 $e |- ( x = A -> B = C ) $.
    $( Introduce an explicit substitution into an implicit substitution
       hypothesis.  See ~ sbhypf for class substitution version. $)
    csbhypf $p |- ( y = A -> [_ y / x ]_ B = C ) $=
      ( vw cv wceq wa wex csb wcel ax-17 visset eqvincf hbcsb1 hbeq csbeq1a
      sylan9req 19.23ai sylbi ) BKZDLAKZUFLZUGDLZMZANAUFEOZFLZACUFDCKUFPAQGBRZS
      UJULAAJCUKFAJUFEUMJKUFPAQTHUAUHUIUKEFAUFEUBIUCUDUE $.
      $( [19-Dec-2008] $) $( [19-Dec-2008] $)
  $}

  ${
    $d x z A $.  $d z B $.  $d y z C $.  $d z D $.  $d x y z $.
    $( Conversion of implicit substitution to explicit substitution into a
       class.  (Closed theorem version of ~ csbiegf .) $)
    csbiegft $p |- ( ( A e. D /\ A. x A. y ( y e. C -> A. x y e. C ) /\
                 A. x ( x = A -> B = C ) ) -> [_ A / x ]_ B = C ) $=
      ( vz wcel cv wal wi wceq w3a wsbc cab csb wb sbciegft id visset eleq1
      albidv imbi12d cla4v 19.20i eleq2 imim2i syl3an abbi1dv df-csb syl5eq )
      CFHZBIZEHZUNAJZKZBJZAJZAICLZDELZKZAJZMZGIZDHZACNZGOEACDPVCVFGEULVDEHZVGAJ
      ZKZAJUSVEVGQZKZAJVFVGQULURVBVEVGACFRULSUQVIAUPVIBVDGTUMVDLZUNVGUOVHUMVDEU
      AZVLUNVGAVMUBUCUDUEVAVKAUTVJUSDEVDUFUGUEUHUIAGCDUJUK $.
      $( [28-Nov-2005] $) $( [11-Nov-2005] $)
  $}

  ${
    $d x z A $.  $d z B $.  $d y C $.  $d x y $.
    csbieb.1 $e |- A e. V $.
    csbieb.2 $e |- ( y e. C -> A. x y e. C ) $.
    $( Bidirectional conversion between an implicit class substitution
       hypothesis ` x = A -> B = C ` and its explicit substitution
       equivalent. $)
    csbieb $p |- ( A. x ( x = A -> B = C ) <-> [_ A / x ]_ B = C ) $=
      ( vz wceq csb cv wcel ax-17 hbcsb1 hbeq csbeq1a eqeq1d ceqsal ) DEIACDJZE
      IACAHBSEAHCDFHKCLAMNGOFAKCIDSEACDPQR $.
      $( [3-Mar-2008] $) $( [2-Mar-2008] $)
  $}

  ${
    $d x y A $.  $d y C $.  $d x y D $.
    csbiegf.1 $e |- ( A e. D -> ( y e. C -> A. x y e. C ) ) $.
    csbiegf.2 $e |- ( x = A -> B = C ) $.
    $( Conversion of implicit substitution to explicit substitution into a
       class. $)
    csbiegf $p |- ( A e. D -> [_ A / x ]_ B = C ) $=
      ( wcel cv wal wi wceq wa csb 19.21aivv ax-gen jctir csbiegft 3expb mpdan
      ) CFIZBJEIZUCAKLZBKAKZAJCMDEMLZAKZNACDOEMZUBUEUGUBUDABGPUFAHQRUBUEUGUHABC
      DEFSTUA $.
      $( [12-Dec-2005] $) $( [11-Nov-2005] $)
  $}

  ${
    $d x A $.  $d y C $.  $d x y $.
    csbief.1 $e |- A e. V $.
    csbief.2 $e |- ( y e. C -> A. x y e. C ) $.
    csbief.3 $e |- ( x = A -> B = C ) $.
    $( Conversion of implicit substitution to explicit substitution into a
       class. $)
    csbief $p |- [_ A / x ]_ B = C $=
      ( cv wceq wi wal csb ax-gen csbieb mpbi ) AICJDEJKZALACDMEJQAHNABCDEFGOP
      $.
      $( [16-Dec-2005] $) $( [26-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.  $d x y z D $.
    csbie2g.1 $e |- A e. V $.
    csbie2g.2 $e |- B e. V $.
    $( Conversion of implicit substitution to explicit substitution into a
       class (closed form of ~ csbie2 ). $)
    csbie2t $p |- ( A. x A. y ( ( x = A /\ y = B ) -> C = D ) ->
                  [_ A / x ]_ [_ B / y ]_ C = D ) $=
      ( vz cv wceq wa wi wal wex csb isseti hba1 wcel ax-17 hbcsb1 hbeq cvv
      hbcsbg ax-mp csbeq1a sylan9eqr pm3.43 mpan eqtr2 syl6 a4s 19.23ad eeanv
      syl5ibr mp2ani ) AJCKZBJDKZLZEFKZMZBNZANZUQAOZURBOZACBDEPZPZFKZACGQBDHQVC
      USBOZAOVHVDVELVCVIVHAVBARAIIVGFAICVFGIJZCSZATUAVJFSZATUBVBVIVHMAVBUSVHBVA
      BRBIIVGFCUCSVJVGSZVMBNMGBAICVFUCVKBTBIDEHVJDSBTUAUDUEVLBTUBVAUSVHMBVAUSEV
      GKZUTLZVHUSVNMVAUSVOMURUQEVFVGBDEUFACVFUFUGUSVNUTUHUIEVGFUJUKULUMULUMUQUR
      ABUNUOUP $.
      $( [9-Sep-2007] $) $( [3-Sep-2007] $)

    ${
      csbie2.3 $e |- ( ( x = A /\ y = B ) -> C = D ) $.
      $( Conversion of implicit substitution to explicit substitution into a
         class. $)
      csbie2 $p |- [_ A / x ]_ [_ B / y ]_ C = D $=
        ( cv wceq wa wi wal csb gen2 csbie2t ax-mp ) AJCKBJDKLEFKMZBNANACBDEOOF
        KSABIPABCDEFGHQR $.
        $( [9-Sep-2007] $) $( [27-Aug-2007] $)
    $}
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.  $d x y z R $.  $d y z S $.
    $( Lemma for ~ csbnestg . $)
    csbnestglem $p |- ( ( A e. R /\ A. x B e. S ) ->
                  [_ A / x ]_ [_ B / y ]_ C = [_ [_ A / x ]_ B / y ]_ C ) $=
      ( vz wcel cv csb wal wi wceq wa csbiegft pm3.26 ax-17 hba1 hban cvv
      csbexg hbcsb1g adantr a1i hbcsbgd mpdan 19.21aiv 19.21ai csbeq1a csbeq1d
      ax-gen syl3anc ) CFIZHJZBACDKZEKZIZURALMZHLZALAJCNZBDEKZUQNMZALZACVBKUQNU
      NDGIZALZOZAHCVBUQFPUNVFQVGUTAUNVFAUNARVEASTZVGUSHVGUPUAIUSACDFGUBVGABHUPE
      UAVHUNVFBUNBRVFBRTUNUOUPIZVIALMVFAHCDFUOCIARUCUDUOEIZVJALMVGVJARUEUFUGUHU
      IVDVGVCAVABDUPEACDUJUKULUEUM $.
      $( [30-Nov-2005] $) $( [23-Nov-2005] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x z C $.  $d w x y $.
    $( Nest the composition of two substitutions. $)
    csbnestg $p |- ( ( A e. R /\ A. x B e. S ) ->
                  [_ A / x ]_ [_ B / y ]_ C = [_ [_ A / x ]_ B / y ]_ C ) $=
      ( vz vw cvv wcel wal csb wceq wa cv csbcog adantr visset csbnestglem
      mpan csbeq2dv ancoms csbexg 19.21aiv sylan2 csbeq1d 3eqtrd eqtr3d hba1
      a4s csbeq2d syl 3eqtr3d elisset 19.20i syl2an ) CJKZDJKZALZACBDEMZMZBACDM
      ZEMZNCFKDGKZALURUTOZACHDBHPEMZMZMZHVCVGMZVBVDVFICAIPZVHMZMZVIVJURVMVINUTA
      ICVHJQRVFVMICHAVKDMZVGMZMZHICVNMZVGMZVJUTURVMVPNUTICVLVOJVKJKZUTVLVONISZA
      HVKDVGJJTUAUBUCURVNJKZILVPVRNUTIHCVNVGJJTUTWAIVSUTWAVTAVKDJJUDUAUEUFURVRV
      JNUTURHVQVCVGAICDJQUGRUHUIUTURVIVBNUTACVHVAJUSAUJUSVHVANABHDEJQUKULUCVFVC
      JKVJVDNACDJJUDBHVCEJQUMUNCFUOVEUSADGUOUPUQ $.
      $( [30-Nov-2005] $) $( [23-Nov-2005] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.  $d y S $.
    $( Nest the composition of two substitutions. $)
    csbnest1g $p |- ( ( A e. R /\ A. x B e. S ) ->
                  [_ A / x ]_ [_ B / x ]_ C = [_ [_ A / x ]_ B / x ]_ C ) $=
      ( vy cvv wcel wal csb wceq cv wi wa csbiegft pm3.26 ax-17 hba1 hban
      csbexg hbcsb1g hbcsb1gd syldan 19.21aiv 19.21ai csbeq1a csbeq1d ax-gen
      a1i syl3anc elisset sylan ) BHIZCFIZAJZABACDKZKAABCKZDKZLZBEIUNGMZUSIZVBA
      JNZGJZAJAMBLZUQUSLNZAJZUTUNUPOZAGBUQUSHPUNUPQVHVDAUNUPAUNARZUOASTVHVCGUNU
      PURHIVCABCHFUAUNAGURDHVIAGBCHVABIARUBUCUDUEUFVGVHVFAVEACURDABCUGUHUIUJUKB
      EULUM $.
      $( [25-May-2006] $) $( [23-May-2006] $)
  $}

  ${
    $d x ph $.  $d x y $.
    $( Nest the composition of two substitutions. $)
    sbcnestg $p |- ( ( A e. R /\ A. x B e. S ) ->
                ( [ A / x ] [ B / y ] ph <-> [ [_ A / x ]_ B / y ] ph ) ) $=
      ( wcel cvv wal wsbc csb wb wa cab hba1 sbccsb2g a4s sbcbid ancoms
      sbcel12g adantr csbnestg eleq2d csbexg syl bitr4d 3bitrd elisset 19.20i
      sylan2 ) DFHZEIHZBJZACEKZBDKZACBDELZKZMEGHZBJULUNNZUPECEACOZLZHZBDKZUQBDV
      BLZHZURUNULUPVDMUNUOVCBDFUMBPUMUOVCMBACEIQRSTULVDVFMUNBDEVBFUAUBUTVFUQCUQ
      VALZHZURUTVEVGUQBCDEVAFIUCUDUTUQIHURVHMBDEFIUEACUQIQUFUGUHUSUMBEGUIUJUK
      $.
      $( [5-Dec-2005] $) $( [27-Nov-2005] $)
  $}

  ${
    $d x y A $.
    $( Idempotent law for class substitutions. $)
    csbidmg $p |- ( A e. C -> [_ A / x ]_ [_ A / x ]_ B = [_ A / x ]_ B ) $=
      ( vy wcel cvv csb wceq elisset wal ax-17 csbnest1g mpdan cv csbconstgf
      csbeq1d eqtrd syl ) BDFBGFZABABCHZHZUAIBDJTUBAABBHZCHZUATTAKUBUDITALABBCG
      GMNTAUCBCAEBBGEOBFALPQRS $.
      $( [7-Mar-2008] $) $( [1-Mar-2008] $)
  $}

  ${
    $d x A $.  $d x C $.  $d x z D $.  $d x y $.
    csbco3g.1 $e |- ( x = A -> B = D ) $.
    $( Composition of two class substitutions. $)
    csbco3g $p |- ( ( A e. R /\ A. x B e. S ) ->
                 [_ A / x ]_ [_ B / y ]_ C = [_ D / y ]_ C ) $=
      ( vz wcel wal wa csb csbnestg wceq cv wi ax-17 gen2 ax-gen csbiegft
      mp3an23 csbeq1d adantr eqtrd ) CGKZDHKALZMACBDENNBACDNZENZBFENZABCDEGHOUG
      UJUKPUHUGBUIFEUGJQFKZULALRZJLALAQCPDFPRZALUIFPUMAJULASTUNAIUAAJCDFGUBUCUD
      UEUF $.
      $( [1-Dec-2005] $) $( [27-Nov-2005] $)
  $}

  ${
    $d x A $.  $d x ph $.  $d x z C $.  $d x y $.
    sbcco3g.1 $e |- ( x = A -> B = C ) $.
    $( Composition of two substitutions. $)
    sbcco3g $p |- ( ( A e. R /\ A. x B e. S ) ->
                ( [ A / x ] [ B / y ] ph <-> [ C / y ] ph ) ) $=
      ( vz wcel wal wa wsbc csb sbcnestg wb wceq cv wi ax-17 gen2 ax-gen
      csbiegft mp3an23 dfsbcq syl adantr bitrd ) DGKZEHKBLZMACENBDNACBDEOZNZACF
      NZABCDEGHPUJUMUNQZUKUJULFRZUOUJJSFKZUQBLTZJLBLBSDREFRTZBLUPURBJUQBUAUBUSB
      IUCBJDEFGUDUEACULFUFUGUHUI $.
      $( [6-Dec-2005] $) $( [27-Nov-2005] $)
  $}

  ${
    $d x B $.  $d x D $.
    $( Special case related to ~ ra4sbc .  (The proof was shortened by Eric
       Schmidt, 17-Jan-2007.) $)
    ra4csbela $p |- ( ( A e. B /\ A. x e. B C e. D ) -> [_ A / x ]_ C e. D ) $=
      ( wcel wral csb wsbc ra4sbc sbcel1g sylibd imp ) BCFZDEFZACGZABDHEFZNPOAB
      IQOABCJABDECKLM $.
      $( [18-Jan-2007] $) $( [10-Dec-2005] $)
  $}

  ${
    $d y z A $.  $d z ph $.  $d x y z $.
    $( Move substitution into a class abstraction. $)
    csbabg $p |- ( A e. B ->
                 [_ A / x ]_ { y | ph } = { y | [ A / x ] ph } ) $=
      ( vz wcel cvv cab csb wsbc wceq elisset cv wb visset sbccomg mpan
      df-clab sbcbii bitr4d abbidv ax-17 hbs1 sbequ12 cbvab syl5eq df-csb
      syl6reqr syl ) DEGDHGZBDACIZJZABDKZCIZLDEMUKUOFNZULGZBDKZFIZUMUKUNCUPKZFI
      USUOUKUTURFUKUTACUPKZBDKZURUPHGUKUTVBOFPACBUPDHHQRUQVABDHAFCSTUAUBUNUTCFU
      NFUCUNCFUDUNCFUEUFUGBFDULUHUIUJ $.
      $( [17-Dec-2005] $) $( [13-Dec-2005] $)
  $}


