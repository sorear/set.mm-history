$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The universal class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the universal class. $)
  $c _V $. $( Letter V (for the universal class) $)

  $( Extend class notation to include the universal class symbol. $)
  cvv $a class _V $.

  ${
    $d z x $.  $d z y $.
    $( Soundness justification theorem for ~ df-v .  (Contributed by Rodolfo
       Medina, 27-Apr-2010.) $)
    vjust $p |- { x | x = x } = { y | y = y } $=
      ( vz cv wceq cab wsbc wcel equid sbt 2th df-clab 3bitr4i eqriv ) CADZOEZA
      FZBDZREZBFZPACDZGZSBUAGZUAQHUATHUBUCPACAIJSBCBIJKPCALSCBLMN $.
      $( [27-Apr-2010] $)
  $}

  $( Define the universal class.  Definition 5.20 of [TakeutiZaring] p. 21. $)
  df-v $a |- _V = { x | x = x } $.

  $( All set variables are sets (see ~ isset ).  Theorem 6.8 of [Quine]
     p. 43. $)
  visset $p |- x e. _V $=
    ( cv cvv wcel wceq eqid df-v abeq2i mpbir ) ABZCDJJEZJFKACAGHI $.
    $( [5-Aug-1993] $)

  ${
    $d x A $.
    $( Two ways to say " ` A ` is a set":  A class ` A ` is a member of the
       universal class ` _V ` (see ~ df-v ) if and only if the class ` A `
       exists (i.e. there exists some set ` x ` equal to class ` A ` ).
       Theorem 6.9 of [Quine] p. 43. _Notational convention_:  We will use the
       notational device " ` A e. _V ` " to mean " ` A ` is a set" very
       frequently, for example in ~ uniex .  Note the when ` A ` is not a set,
       it is called a proper class.  In some theorems, such as ~ uniexg , in
       order to shorten certain proofs we use the more general antecedent
       ` A e. V ` instead of ` A e. _V ` to mean " ` A ` is a set."

       Note that a constant is implicitly considered distinct from all
       variables.  This is why ` _V ` is not included in the distinct variable
       list, even though ~ df-clel requires that the expression substituted for
       ` B ` not contain ` x ` .  (Also, the Metamath spec does not allow
       constants in the distinct variable list.) $)
    isset $p |- ( A e. _V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wa wex df-clel visset biantru exbii bitr4i ) BCDAEZBFZ
      NCDZGZAHOAHABCIOQAPOAJKLM $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d A y $.  $d x y $.
    issetf.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( A version of isset that does not require x and A to be distinct.
       (Contributed by Andrew Salmon, 6-Jun-2011.) $)
    issetf $p |- ( A e. _V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wex isset hbeleq ax-17 eqeq1 cbvex bitri ) CEFBGZCHZBI
      AGZCHZAIBCJQSBAABCDKSBLPRCMNO $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d A y $.  $d x y $.
    issetfOLD.1 $e |- ( y e. A -> A. x y e. A ) $.
    issetfOLD.2 $e |- ( x = A -> A. y x = A ) $.
    $( A version of isset that does not require x and A to be distinct.
       (Contributed by Andrew Salmon, 6-Jun-2011.) $)
    issetfOLD $p |- ( A e. _V <-> E. x x = A ) $=
      ( issetf ) ABCDF $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x A $.
    isseti.1 $e |- A e. _V $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    isseti $p |- E. x x = A $=
      ( cvv wcel cv wceq wex isset mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    issetri.1 $e |- E. x x = A $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    issetri $p |- A e. _V $=
      ( cvv wcel cv wceq wex isset mpbir ) BDEAFBGAHCABIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( If a class is a member of another class, it is a set.  Theorem 6.12 of
       [Quine] p. 44.  (The proof was shortened by Andrew Salmon,
       8-Jun-2011.) $)
    elisset $p |- ( A e. B -> A e. _V ) $=
      ( vx cv wceq wcel wa wex cvv exsimpl df-clel isset 3imtr4i ) CDZAEZNBFZGC
      HOCHABFAIFOPCJCABKCALM $.
      $( [8-Jun-2011] $) $( [5-Aug-1993] $)
  $}

  ${
    elisseti.1 $e |- A e. B $.
    $( If a class is a member of another class, it is a set. $)
    elisseti $p |- A e. _V $=
      ( wcel cvv elisset ax-mp ) ABDAEDCABFG $.
      $( [11-Jun-1994] $)
  $}

  ${
    $d x A $.
    $( An element of a class exists. $)
    elex $p |- ( A e. V -> E. x x = A ) $=
      ( wcel cvv cv wceq wex elisset isset sylib ) BCDBEDAFBGAHBCIABJK $.
      $( [1-May-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( If two classes each contain another class, then both contain some set.
       This proof was automatically generated from the virtual deduction proof
       ~ elex22VD using a translation program.  (Contributed by Alan Sare,
       24-Oct-2011.) $)
    elex22 $p |- ( ( A e. B /\ A e. C ) -> E. x ( x e. B /\ x e. C ) ) $=
      ( wcel wa cv wceq wi wal wex eleq1a anim12ii 19.21aiv elex adantr exim
      sylc ) BCEZBDEZFZAGZBHZUBCEZUBDEZFZIZAJUCAKZUFAKUAUGASUCUDTUEBCUBLBDUBLMN
      SUHTABCOPUCUFAQR $.
      $( [24-Oct-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( If a class contains another class, then it contains some set.  This
       proof was generated automatically from the virtual deduction proof
       ~ elex2 using the tools command file
       translate_without_overwriting_minimize_excluding_duplicates.cmd .
       (Contributed by Alan Sare, 25-Sep-2011.) $)
    elex2 $p |- ( A e. B -> E. x x e. B ) $=
      ( wcel cv wceq wi wal wex eleq1a 19.21aiv elex exim sylc ) BCDZAEZBFZPCDZ
      GZAHQAIRAIOSABCPJKABCLQRAMN $.
      $( [24-Oct-2011] $) $( [25-Sep-2011] $)
  $}

  $( A universal quantifier restricted to the universe is unrestricted. $)
  ralv $p |- ( A. x e. _V ph <-> A. x ph ) $=
    ( cvv wral cv wcel wi wal df-ral visset a1bi albii bitr4i ) ABCDBECFZAGZBHA
    BHABCIAOBNABJKLM $.
    $( [26-Mar-2004] $)

  $( An existential quantifier restricted to the universe is unrestricted. $)
  rexv $p |- ( E. x e. _V ph <-> E. x ph ) $=
    ( cvv wrex cv wcel wa wex df-rex visset biantrur exbii bitr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [26-Mar-2004] $)

  $( A uniqueness quantifier restricted to the universe is unrestricted. $)
  reuv $p |- ( E! x e. _V ph <-> E! x ph ) $=
    ( cvv wreu cv wcel wa weu df-reu visset biantrur eubii bitr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [7-Jun-2011] $) $( [1-Nov-2010] $)

  $( A class abstraction restricted to the universe is unrestricted.  (The
     proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
  rabab $p |- { x e. _V | ph } = { x | ph } $=
    ( cvv crab cv wcel wa cab df-rab visset biantrur abbii eqtr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [8-Jun-2011] $) $( [27-Dec-2004] $)

  ${
    $d x y $.  $d y A $.
    $( Commutation of restricted and unrestricted universal quantifiers.  (The
       proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
    ralcom4 $p |- ( A. x e. A A. y ph <-> A. y A. x e. A ph ) $=
      ( cvv wral wal ralcom ralv ralbii 3bitr3i ) ACEFZBDFABDFZCEFACGZBDFMCGABC
      DEHLNBDACIJMCIK $.
      $( [8-Jun-2011] $) $( [26-Mar-2004] $)

    $( Commutation of restricted and unrestricted existential quantifiers.
       (The proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
    rexcom4 $p |- ( E. x e. A E. y ph <-> E. y E. x e. A ph ) $=
      ( cvv wrex wex rexcom rexv rexbii 3bitr3i ) ACEFZBDFABDFZCEFACGZBDFMCGABC
      DEHLNBDACIJMCIK $.
      $( [8-Jun-2011] $) $( [12-Apr-2004] $)
  $}

  ${
    $d A x $.  $d x y $.  $d ph x $.
    $( Specialized existential commutation lemma.  (Contributed by Jeff Madsen,
       1-Jun-2011.) $)
    rexcom4a $p |- ( E. x E. y e. A ( ph /\ ps )
                          <-> E. y e. A ( ph /\ E. x ps ) ) $=
      ( wa wrex wex rexcom4 19.42v rexbii bitr3i ) ABFZDEGCHMCHZDEGABCHFZDEGMDC
      EINODEABCJKL $.
      $( [1-Jun-2011] $)

    $d B x $.
    rexcom4b.1 $e |- B e. _V $.
    $( Specialized existential commutation lemma.  (Contributed by Jeff Madsen,
       1-Jun-2011.) $)
    rexcom4b $p |- ( E. x E. y e. A ( ph /\ x = B ) <-> E. y e. A ph ) $=
      ( cv wceq wa wrex wex rexcom4a isseti biantru rexbii bitr4i ) ABGEHZICDJB
      KAQBKZIZCDJACDJAQBCDLASCDRABEFMNOP $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d x A $.
    $( Closed theorem version of ~ ceqsalg . $)
    ceqsalt $p |- ( ( A. x ( ps -> A. x ps )
              /\ A. x ( x = A -> ( ph <-> ps ) ) /\ A e. V )
         -> ( A. x ( x = A -> ph ) <-> ps ) ) $=
      ( wal wi cv wceq wb wcel w3a wex elex 3ad2ant3 bi1 imim3i al2imi
      3ad2ant2 19.23t 3ad2ant1 sylibd mpid bi2 imim2i com23 alimi 19.21t mpbid
      impbid ) BBCFGCFZCHDIZABJZGZCFZDEKZLZULAGZCFZBUQUSULCMZBUPUKUTUOCDENOUQUS
      ULBGZCFZUTBGZUOUKUSVBGUPUNURVACUMABULABPQRSUKUOVBVCJUPULBCTUAUBUCUQBURGZC
      FZBUSGZUOUKVEUPUNVDCUNULBAUMBAGULABUDUEUFUGSUKUOVEVFJUPBURCUHUAUIUJ $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Restricted quantifier version of ~ ceqsalt . $)
    ceqsralt $p |- ( ( A. x ( ps -> A. x ps )
              /\ A. x ( x = A -> ( ph <-> ps ) ) /\ A e. B )
         -> ( A. x e. B ( x = A -> ph ) <-> ps ) ) $=
      ( wal wi cv wceq wb wcel w3a wral df-ral wa eleq1 pm5.32ri imbi1i impexp
      3bitr3i albii a1i syl5bb 19.21v syl6bb biimt 3ad2ant3 ceqsalt 3bitr2d )
      BBCFGCFZCHZDIZABJGCFZDEKZLZULAGZCEMZUNUPCFZGZURBUOUQUNUPGZCFZUSUQUKEKZUPG
      ZCFZUOVAUPCENVDVAJUOVCUTCVBULOZAGUNULOZAGVCUTVEVFAULVBUNUKDEPQRVBULASUNUL
      ASTUAUBUCUNUPCUDUEUNUJURUSJUMUNURUFUGABCDEUHUI $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d x A $.
    ceqsalg.1 $e |- ( ps -> A. x ps ) $.
    ceqsalg.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    ceqsalg $p |- ( A e. V -> ( A. x ( x = A -> ph ) <-> ps ) ) $=
      ( wcel cv wceq wi wal wex elex hba1 biimpd a2i a4s 19.23ad syl5com
      biimprcd 19.21ai impbid1 ) DEHZCIDJZAKZCLZBUDUECMUGBCDENUGUEBCUFCOFUFUEBK
      CUEABUEABGPQRSTBUFCFUEABGUAUBUC $.
      $( [8-Jun-2011] $) $( [29-Oct-2003] $)
  $}

  ${
    $d x A $.
    ceqsal.1 $e |- ( ps -> A. x ps ) $.
    ceqsal.2 $e |- A e. _V $.
    ceqsal.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsal $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( cvv wcel cv wceq wi wal wb ceqsalg ax-mp ) DHICJDKALCMBNFABCDHEGOP $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsalv.1 $e |- A e. _V $.
    ceqsalv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsalv $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( ax-17 ceqsal ) ABCDBCGEFH $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    ceqsralv.1 $e |- A e. _V $.
    ceqsralv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted quantifier version of ~ ceqsalv . $)
    ceqsralv $p |- ( A e. B -> ( A. x e. B ( x = A -> ph ) <-> ps ) ) $=
      ( wal wi cv wceq wb wcel wral ax-17 ax-gen ceqsralt mp3an12 ) BBCHIZCHCJD
      KZABLIZCHDEMTAICENBLSCBCOPUACGPABCDEQR $.
      $( [21-Jun-2013] $)
  $}

  ${
    $d x ps $.
    gencl.1 $e |- ( th <-> E. x ( ch /\ A = B ) ) $.
    gencl.2 $e |- ( A = B -> ( ph <-> ps ) ) $.
    gencl.3 $e |- ( ch -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    gencl $p |- ( th -> ps ) $=
      ( wceq wa wex syl5ib impcom 19.23aiv sylbi ) DCFGKZLZEMBHSBERCBCARBJINOPQ
      $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y $.  $d x R $.  $d x ps $.  $d y C $.  $d y S $.  $d y ch $.
    2gencl.1 $e |- ( C e. S <-> E. x e. R A = C ) $.
    2gencl.2 $e |- ( D e. S <-> E. y e. R B = D ) $.
    2gencl.3 $e |- ( A = C -> ( ph <-> ps ) ) $.
    2gencl.4 $e |- ( B = D -> ( ps <-> ch ) ) $.
    2gencl.5 $e |- ( ( x e. R /\ y e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    2gencl $p |- ( ( C e. S /\ D e. S ) -> ch ) $=
      ( wcel wi cv wceq wrex wa wex df-rex bitri imbi2d ex gencl com12 impcom
      ) IKQZHKQZCULBRULCRESJQZUKEGIUKGITZEJUAUMUNUBEUCMUNEJUDUEUNBCULOUFULUMBUM
      ARUMBRDSJQZULDFHULFHTZDJUAUOUPUBDUCLUPDJUDUEUPABUMNUFUOUMAPUGUHUIUHUJ $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y $.  $d x R $.  $d x ps $.  $d y C $.  $d y S $.  $d y ch $.
    2genclOLD.1 $e |- ( C e. S <-> E. x ( x e. R /\ A = C ) ) $.
    2genclOLD.2 $e |- ( D e. S <-> E. y ( y e. R /\ B = D ) ) $.
    2genclOLD.3 $e |- ( A = C -> ( ph <-> ps ) ) $.
    2genclOLD.4 $e |- ( B = D -> ( ps <-> ch ) ) $.
    2genclOLD.5 $e |- ( ( x e. R /\ y e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    2genclOLD $p |- ( ( C e. S /\ D e. S ) -> ch ) $=
      ( wcel wi cv wceq imbi2d ex gencl com12 impcom ) IKQZHKQZCUGBRUGCRESJQZUF
      EGIMGITBCUGOUAUGUHBUHARUHBRDSJQZUGDFHLFHTABUHNUAUIUHAPUBUCUDUCUE $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y z $.  $d y z D $.  $d z F $.  $d x y R $.  $d y z S $.  $d x ps $.
    $d y ch $.  $d z th $.
    3gencl.1 $e |- ( D e. S <-> E. x e. R A = D ) $.
    3gencl.2 $e |- ( F e. S <-> E. y e. R B = F ) $.
    3gencl.3 $e |- ( G e. S <-> E. z e. R C = G ) $.
    3gencl.4 $e |- ( A = D -> ( ph <-> ps ) ) $.
    3gencl.5 $e |- ( B = F -> ( ps <-> ch ) ) $.
    3gencl.6 $e |- ( C = G -> ( ch <-> th ) ) $.
    3gencl.7 $e |- ( ( x e. R /\ y e. R /\ z e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    3gencl $p |- ( ( D e. S /\ F e. S /\ G e. S ) -> th ) $=
      ( wcel wa wi cv wceq wrex wex df-rex bitri imbi2d 3expia 2gencl com12
      gencl 3impia ) KMUCZNMUCZOMUCZDUTURUSUDZDVACUEVADUEGUFLUCZUTGJOUTJOUGZGLU
      HVBVCUDGUIRVCGLUJUKVCCDVAUAULVAVBCVBAUEVBBUEVBCUEEFHIKNLMPQHKUGABVBSULINU
      GBCVBTULEUFLUCFUFLUCVBAUBUMUNUOUPUOUQ $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y z $.  $d y z D $.  $d z F $.  $d x y R $.  $d y z S $.  $d x ps $.
    $d y ch $.  $d z th $.
    3genclOLD.1 $e |- ( D e. S <-> E. x ( x e. R /\ A = D ) ) $.
    3genclOLD.2 $e |- ( F e. S <-> E. y ( y e. R /\ B = F ) ) $.
    3genclOLD.3 $e |- ( G e. S <-> E. z ( z e. R /\ C = G ) ) $.
    3genclOLD.4 $e |- ( A = D -> ( ph <-> ps ) ) $.
    3genclOLD.5 $e |- ( B = F -> ( ps <-> ch ) ) $.
    3genclOLD.6 $e |- ( C = G -> ( ch <-> th ) ) $.
    3genclOLD.7 $e |- ( ( x e. R /\ y e. R /\ z e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    3genclOLD $p |- ( ( D e. S /\ F e. S /\ G e. S ) -> th ) $=
      ( wcel wa wi cv wceq imbi2d 3expia 2genclOLD com12 gencl 3impia ) KMUCZNM
      UCZOMUCZDUPUNUOUDZDUQCUEUQDUEGUFLUCZUPGJORJOUGCDUQUAUHUQURCURAUEURBUEURCU
      EEFHIKNLMPQHKUGABURSUHINUGBCURTUHEUFLUCFUFLUCURAUBUIUJUKULUKUM $.
      $( [17-May-1996] $)
  $}

  ${
    $d x A $.  $d x ps $.
    cgsexg.1 $e |- ( x = A -> ch ) $.
    cgsexg.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsexg $p |- ( A e. V ->
                     ( E. x ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aiv cv wceq elex eximi syl biimprcd ancld
      eximdv syl5com impbid2 ) EFIZCAJZDKZBUEBDCABHLMUDCDKZBUFUDDNEOZDKUGDEFPUH
      CDGQRBCUEDBCACABHSTUAUBUC $.
      $( [21-Apr-2008] $) $( [26-Aug-2007] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    cgsex2g.1 $e |- ( ( x = A /\ y = B ) -> ch ) $.
    cgsex2g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsex2g $p |- ( ( A e. V /\ B e. W ) ->
                     ( E. x E. y ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aivv cv wceq elex anim12i eeanv sylibr 2eximi
      syl biimprcd ancld 2eximdv syl5com impbid2 ) FHLZGILZMZCAMZENDNZBUMBDECAB
      KOPULCENDNZBUNULDQFRZEQGRZMZENDNZUOULUPDNZUQENZMUSUJUTUKVADFHSEGISTUPUQDE
      UAUBURCDEJUCUDBCUMDEBCACABKUEUFUGUHUI $.
      $( [26-Jul-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $.
    cgsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) -> ch ) $.
    cgsex4g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 4 general classes. $)
    cgsex4g $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                    ( E. x E. y E. z E. w ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aivv cv wceq elex anim12i eeanv sylibr ee4anv
      2eximi syl biimprcd ancld 2eximdv syl5com impbid2 ) HLPZIMPZQZJLPZKMPZQZQ
      ZCAQZGRFRZERDRZBVCBDEVBBFGCABOSTTVACGRFRZERDRZBVDVADUAHUBZEUAIUBZQZFUAJUB
      ZGUAKUBZQZQZGRFRZERDRZVFVAVIERDRZVLGRFRZQVOUQVPUTVQUQVGDRZVHERZQVPUOVRUPV
      SDHLUCEIMUCUDVGVHDEUEUFUTVJFRZVKGRZQVQURVTUSWAFJLUCGKMUCUDVJVKFGUEUFUDVIV
      LDEFGUGUFVNVEDEVMCFGNUHUHUIBVEVCDEBCVBFGBCACABOUJUKULULUMUN $.
      $( [5-Aug-1995] $)
  $}

  ${
    $d x A $.
    ceqsex.1 $e |- ( ps -> A. x ps ) $.
    ceqsex.2 $e |- A e. _V $.
    ceqsex.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsex $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( cv wceq wa wex biimpa 19.23ai isseti wi wal biimprcd 19.21ai exintr
      syl mpi impbii ) CHDIZAJZCKZBUDBCEUCABGLMBUCCKZUECDFNBUCAOZCPUFUEOBUGCEUC
      ABGQRUCACSTUAUB $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsexv.1 $e |- A e. _V $.
    ceqsexv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexv $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( ax-17 ceqsex ) ABCDBCGEFH $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    ceqsex2.1 $e |- ( ps -> A. x ps ) $.
    ceqsex2.2 $e |- ( ch -> A. y ch ) $.
    ceqsex2.3 $e |- A e. _V $.
    ceqsex2.4 $e |- B e. _V $.
    ceqsex2.5 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex2.6 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2 $p |- ( E. x E. y ( x = A /\ y = B /\ ph ) <-> ch ) $=
      ( cv wceq w3a wex wa 3anass exbii 19.42v bitri ax-17 hban hbex anbi2d
      exbidv ceqsex 3bitri ) DNFOZENGOZAPZEQZDQUJUKARZEQZRZDQUKBRZEQZCUMUPDUMUJ
      UNRZEQUPULUSEUJUKASTUJUNEUAUBTUOURDFUQDEUKBDUKDUCHUDUEJUJUNUQEUJABUKLUFUG
      UHBCEGIKMUHUI $.
      $( [13-Aug-2011] $) $( [7-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ps $.  $d y ch $.
    ceqsex2v.1 $e |- A e. _V $.
    ceqsex2v.2 $e |- B e. _V $.
    ceqsex2v.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex2v.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2v $p |- ( E. x E. y ( x = A /\ y = B /\ ph ) <-> ch ) $=
      ( ax-17 ceqsex2 ) ABCDEFGBDLCELHIJKM $.
      $( [13-Aug-2011] $) $( [7-Jun-2006] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x ps $.  $d y ch $.
    $d z th $.
    ceqsex3v.1 $e |- A e. _V $.
    ceqsex3v.2 $e |- B e. _V $.
    ceqsex3v.3 $e |- C e. _V $.
    ceqsex3v.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex3v.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex3v.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    $( Elimination of three existential quantifiers, using implicit
       substitution. $)
    ceqsex3v $p |- ( E. x E. y E. z ( ( x = A /\ y = B /\ z = C ) /\ ph )
                 <-> th ) $=
      ( cv wceq w3a wa wex anass 3anass anbi1i df-3an anbi2i 3bitr4i 2exbii
      19.42vv bitri exbii 3anbi3d 2exbidv ceqsexv ceqsex2v 3bitri ) EQHRZFQIRZG
      QJRZSZATZGUAFUAZEUAUQURUSASZGUAFUAZTZEUAURUSBSZGUAFUAZDVBVEEVBUQVCTZGUAFU
      AVEVAVHFGUQURUSTZTZATUQVIATZTVAVHUQVIAUBUTVJAUQURUSUCUDVCVKUQURUSAUEUFUGU
      HUQVCFGUIUJUKVDVGEHKUQVCVFFGUQABURUSNULUMUNBCDFGIJLMOPUOUP $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x ps $.  $d y ch $.  $d z th $.  $d w ta $.
    ceqsex4v.1 $e |- A e. _V $.
    ceqsex4v.2 $e |- B e. _V $.
    ceqsex4v.3 $e |- C e. _V $.
    ceqsex4v.4 $e |- D e. _V $.
    ceqsex4v.7 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex4v.8 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex4v.9 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex4v.10 $e |- ( w = D -> ( th <-> ta ) ) $.
    $( Elimination of four existential quantifiers, using implicit
       substitution. $)
    ceqsex4v $p |- ( E. x E. y E. z E. w
          ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) /\ ph ) <-> ta ) $=
      ( cv wceq wa w3a wex 19.42vv 3anass df-3an anbi2i bitr4i 2exbii 3bitr4i
      3anbi3d 2exbidv ceqsex2v 3bitri ) FUBJUCZGUBKUCZUDZHUBLUCZIUBMUCZUDZAUEZI
      UFHUFZGUFFUFURUSVAVBAUEZIUFHUFZUEZGUFFUFVAVBCUEZIUFHUFZEVEVHFGUTVFUDZIUFH
      UFUTVGUDVEVHUTVFHIUGVDVKHIVDUTVCAUDZUDVKUTVCAUHVFVLUTVAVBAUIUJUKULURUSVGU
      IUMULVGVAVBBUEZIUFHUFVJFGJKNOURVFVMHIURABVAVBRUNUOUSVMVIHIUSBCVAVBSUNUOUP
      CDEHILMPQTUAUPUQ $.
      $( [23-Sep-2011] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u E $.  $d x y z w v u F $.  $d x ps $.
    $d y ch $.  $d z th $.  $d w ta $.  $d v et $.  $d u ze $.
    ceqsex6v.1 $e |- A e. _V $.
    ceqsex6v.2 $e |- B e. _V $.
    ceqsex6v.3 $e |- C e. _V $.
    ceqsex6v.4 $e |- D e. _V $.
    ceqsex6v.5 $e |- E e. _V $.
    ceqsex6v.6 $e |- F e. _V $.
    ceqsex6v.7 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex6v.8 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex6v.9 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex6v.10 $e |- ( w = D -> ( th <-> ta ) ) $.
    ceqsex6v.11 $e |- ( v = E -> ( ta <-> et ) ) $.
    ceqsex6v.12 $e |- ( u = F -> ( et <-> ze ) ) $.
    $( Elimination of six existential quantifiers, using implicit
       substitution. $)
    ceqsex6v $p |- ( E. x E. y E. z E. w E. v E. u
          ( ( x = A /\ y = B /\ z = C ) /\ ( w = D /\ v = E /\ u = F ) /\ ph )
                 <-> ze ) $=
      ( cv wceq w3a wex wa 3anass 3exbii 19.42vvv bitri anbi2d 3exbidv
      ceqsex3v 3bitri ) HULNUMZIULOUMZJULPUMZUNZKULQUMLULRUMMULSUMUNZAUNZMUOLUO
      KUOZJUOIUOHUOVHVIAUPZMUOLUOKUOZUPZJUOIUOHUOVIDUPZMUOLUOKUOZGVKVNHIJVKVHVL
      UPZMUOLUOKUOVNVJVQKLMVHVIAUQURVHVLKLMUSUTURVMVIBUPZMUOLUOKUOVICUPZMUOLUOK
      UOVPHIJNOPTUAUBVEVLVRKLMVEABVIUFVAVBVFVRVSKLMVFBCVIUGVAVBVGVSVOKLMVGCDVIU
      HVAVBVCDEFGKLMQRSUCUDUEUIUJUKVCVD $.
      $( [21-Sep-2011] $)
  $}

  ${
    $d x y z w v u t s A $.  $d x y z w v u t s B $.  $d x y z w v u t s C $.
    $d x y z w v u t s D $.  $d x y z w v u t s E $.  $d x y z w v u t s F $.
    $d x y z w v u t s G $.  $d x y z w v u t s H $.  $d x ps $.  $d y ch $.
    $d z th $.  $d w ta $.  $d v et $.  $d u ze $.  $d t si $.  $d s rh $.
    ceqsex8v.1 $e |- A e. _V $.
    ceqsex8v.2 $e |- B e. _V $.
    ceqsex8v.3 $e |- C e. _V $.
    ceqsex8v.4 $e |- D e. _V $.
    ceqsex8v.5 $e |- E e. _V $.
    ceqsex8v.6 $e |- F e. _V $.
    ceqsex8v.7 $e |- G e. _V $.
    ceqsex8v.8 $e |- H e. _V $.
    ceqsex8v.9 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex8v.10 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex8v.11 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex8v.12 $e |- ( w = D -> ( th <-> ta ) ) $.
    ceqsex8v.13 $e |- ( v = E -> ( ta <-> et ) ) $.
    ceqsex8v.14 $e |- ( u = F -> ( et <-> ze ) ) $.
    ceqsex8v.15 $e |- ( t = G -> ( ze <-> si ) ) $.
    ceqsex8v.16 $e |- ( s = H -> ( si <-> rh ) ) $.
    $( Elimination of eight existential quantifiers, using implicit
       substitution. $)
    ceqsex8v $p |- ( E. x E. y E. z E. w E. v E. u E. t E. s
              ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) )
         /\ ( ( v = E /\ u = F ) /\ ( t = G /\ s = H ) ) /\ ph ) <-> rh ) $=
      ( cv wceq wa w3a wex 19.42vv 2exbii bitri 3anass df-3an anbi2i bitr4i
      3bitr4i 3anbi3d 4exbidv ceqsex4v 3bitri ) JVBQVCZKVBRVCZVDZLVBSVCZMVBTVCZ
      VDZVDZNVBUAVCOVBUBVCVDZPVBUCVCUEVBUDVCVDZVDZAVEZUEVFPVFZOVFNVFZMVFLVFZKVF
      JVFWAWDWFWGAVEZUEVFPVFZOVFNVFZVEZMVFLVFZKVFJVFWFWGEVEZUEVFPVFOVFNVFZIWLWQ
      JKWKWPLMWEWMVDZUEVFPVFZOVFNVFZWEWOVDZWKWPXBWEWNVDZOVFNVFXCXAXDNOWEWMPUEVG
      VHWEWNNOVGVIWJXANOWIWTPUEWIWEWHAVDZVDWTWEWHAVJWMXEWEWFWGAVKVLVMVHVHWAWDWO
      VKVNVHVHWOWFWGBVEZUEVFPVFOVFNVFWFWGCVEZUEVFPVFOVFNVFWFWGDVEZUEVFPVFOVFNVF
      WSJKLMQRSTUFUGUHUIVSWMXFNOPUEVSABWFWGUNVOVPVTXFXGNOPUEVTBCWFWGUOVOVPWBXGX
      HNOPUEWBCDWFWGUPVOVPWCXHWRNOPUEWCDEWFWGUQVOVPVQEFGHINOPUEUAUBUCUDUJUKULUM
      URUSUTVAVQVR $.
      $( [23-Sep-2011] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex.1 $e |- A e. _V $.
    gencbvex.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    gencbvex $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex excom wb anbi12d bicomd eqcoms ceqsexv exbii 19.41v
      simpr eqcom biimpi adantl eximi sylbi adantr ancri impbii bitri 3bitr3i
      ) FLZGMZDBNZNZFOZEOUREOZFOCANZEOUQFOUREFPUSVAEUQVAFGHUQVAQGUOGUOMZVAUQVBC
      DABJIRSTUAUBUTUQFUTUPEOZUQNZUQUPUQEUCVDUQVCUQUDUQVCDVCBDCVBNZEOVCKVEUPEVB
      UPCVBUPGUOUEUFUGUHUIUJUKULUMUBUN $.
      $( [8-Jun-2011] $) $( [17-May-1996] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex2.1 $e |- A e. _V $.
    gencbvex2.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex2.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex2.4 $e |- ( th -> E. x ( ch /\ A = y ) ) $.
    $( Restatement of ~ gencbvex with weaker hypotheses.  (Contributed by
       Jeffrey Hankins, 6-Dec-2006.) $)
    gencbvex2 $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex biimpac 19.23aiv impbii gencbvex ) ABCDEFGHIJDCGFLMZNZEO
      KUADETCDJPQRS $.
      $( [6-Dec-2006] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbval.1 $e |- A e. _V $.
    gencbval.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbval.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbval.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbval $p |- ( A. x ( ch -> ph ) <-> A. y ( th -> ps ) ) $=
      ( wi wal wn wa wex cv wceq notbid gencbvex exanali 3bitr3i con4bii ) CALE
      MZDBLFMZCANZOEPDBNZOFPUDNUENUFUGCDEFGHGFQRABISJKTCAEUADBFUAUBUC $.
      $( [17-May-1996] $)
  $}

  ${
    $d A x $.  $d x y $.
    sbhypf.1 $e |- ( ps -> A. x ps ) $.
    sbhypf.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Introduce an explicit substitution into an implicit substitution
       hypothesis.  See also ~ csbhypf .  (Contributed by Raph Levien,
       10-Apr-2004.) $)
    sbhypf $p |- ( y = A -> ( [ y / x ] ph <-> ps ) ) $=
      ( cv wceq wa wex wsbc wb visset eqeq1 ceqsexv hbs1 hbbi sbequ12 bicomd
      sylan9bb 19.23ai sylbir ) DHZEIZCHZUDIZUFEIZJZCKACUDLZBMZUHUECUDDNUFUDEOP
      UIUKCUJBCACDQFRUGUJAUHBUGAUJACDSTGUAUBUC $.
      $( [5-Mar-2008] $) $( 2-Mar-2008] $)
  $}

  ${
    $d x A $.
    vtoclf.1 $e |- ( ps -> A. x ps ) $.
    vtoclf.2 $e |- A e. _V $.
    vtoclf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclf.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable.  This is a
       generalization of ~ chvar . $)
    vtoclf $p |- ps $=
      ( cv wceq wex wi isseti biimpd eximi ax-mp 19.36i mpg ) ABCABCECIDJZCKABL
      ZCKCDFMSTCSABGNOPQHR $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ps $.
    vtocl.1 $e |- A e. _V $.
    vtocl.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl.3 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl $p |- ps $=
      ( ax-17 vtoclf ) ABCDBCHEFGI $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    vtocl2.1 $e |- A e. _V $.
    vtocl2.2 $e |- B e. _V $.
    vtocl2.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    vtocl2.4 $e |- ph $.
    $( Implicit substitution of classes for set variables.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    vtocl2 $p |- ps $=
      ( wal wi wex cv wceq isseti wa eeanv biimpd 2eximi sylbir mp2an 19.36v
      exbii mpbi 19.36aiv ax-gen mpg ) ADKZBCUIBCABLZDMZCMZUIBLZCMCNEOZCMZDNFOZ
      DMZULCEGPDFHPUOUQQUNUPQZDMCMULUNUPCDRURUJCDURABISTUAUBUKUMCABDUCUDUEUFADJ
      UGUH $.
      $( [8-Jun-2011] $) $( [26-Jul-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    vtocl3.1 $e |- A e. _V $.
    vtocl3.2 $e |- B e. _V $.
    vtocl3.3 $e |- C e. _V $.
    vtocl3.4 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    vtocl3.5 $e |- ph $.
    $( Implicit substitution of classes for set variables.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    vtocl3 $p |- ps $=
      ( wal wi wex cv wceq isseti w3a eeeanv biimpd eximi 2eximi sylbir mp3an
      19.36v 2exbii mpbi exbii 19.36aiv gen2 mpg ) AENZDNZBCUOBCUNBOZDPZCPZUOBO
      ZCPABOZEPZDPCPZURCQFRZCPZDQGRZDPZEQHRZEPZVBCFISDGJSEHKSVDVFVHTVCVEVGTZEPZ
      DPCPVBVCVEVGCDEUAVJVACDVIUTEVIABLUBUCUDUEUFVAUPCDABEUGUHUIUQUSCUNBDUGUJUI
      UKADEMULUM $.
      $( [8-Jun-2011] $) $( [3-Jun-1995] $)
  $}

  ${
    $d x A $.  $d x ch $.  $d x th $.
    vtoclb.1 $e |- A e. _V $.
    vtoclb.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclb.3 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclb.4 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclb $p |- ( ch <-> th ) $=
      ( wb cv wceq bibi12d vtocl ) ABKCDKEFGELFMACBDHINJO $.
      $( [23-Dec-1993] $)
  $}

  ${
    $d y A z $.  $d x z $.  $d y z $.
    vtoclgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgf.2 $e |- ( ps -> A. x ps ) $.
    vtoclgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgf.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable, with bound-variable
       hypotheses in place of distinct variable restrictions.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       11-Jul-2011.) $)
    vtoclgf $p |- ( A e. V -> ps ) $=
      ( vz wcel cvv elisset cv wceq wex isset hblem hbeleq ax-17 eqeq1 cbvex
      bitri mpbii 19.23ai sylbi syl ) EFLEMLZBEFNUICOZEPZCQZBUIKOZEPZKQULKERUNU
      KKCCKECDKEGSTUKKUAUMUJEUBUCUDUKBCHUKABJIUEUFUGUH $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    vtoclg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclg.2 $e |- ph $.
    $( Implicit substitution of a class expression for a set variable. $)
    vtoclg $p |- ( A e. V -> ps ) $=
      ( vy cv wcel ax-17 vtoclgf ) ABCHDEHIDJCKBCKFGL $.
      $( [17-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x ch $.  $d x th $.
    vtoclbg.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclbg.2 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclbg.3 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclbg $p |- ( A e. V -> ( ch <-> th ) ) $=
      ( wb cv wceq bibi12d vtoclg ) ABKCDKEFGELFMACBDHINJO $.
      $( [29-Apr-1994] $)
  $}

  ${
    $d z A $.  $d z B $.  $d z x $.  $d z y $.
    vtocl2gf.1 $e |- ( z e. A -> A. x z e. A ) $.
    vtocl2gf.2 $e |- ( z e. A -> A. y z e. A ) $.
    vtocl2gf.3 $e |- ( z e. B -> A. y z e. B ) $.
    vtocl2gf.4 $e |- ( ps -> A. x ps ) $.
    vtocl2gf.5 $e |- ( ch -> A. y ch ) $.
    vtocl2gf.6 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gf.7 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gf.8 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl2gf $p |- ( ( A e. V /\ B e. W ) -> ch ) $=
      ( wcel cvv elisset wi cv ax-17 hbel hbim wceq imbi2d vtoclgf impcom
      sylan ) GISGTSZHJSZCGIUAUMULCULBUBULCUBEFHJMULCEEFFGTLFUCTSEUDUEOUFEUCHUG
      BCULQUHABDFGTKNPRUIUIUJUK $.
      $( [10-Aug-2013] $) $( [25-Apr-1995] $)
  $}

  ${
    $d w A $.  $d w B $.  $d w C $.  $d w y $.  $d w z $.
    vtocl3gf.a $e |- ( w e. A -> A. x w e. A ) $.
    vtocl3gf.b $e |- ( w e. A -> A. y w e. A ) $.
    vtocl3gf.c $e |- ( w e. A -> A. z w e. A ) $.
    vtocl3gf.d $e |- ( w e. B -> A. y w e. B ) $.
    vtocl3gf.e $e |- ( w e. B -> A. z w e. B ) $.
    vtocl3gf.f $e |- ( w e. C -> A. z w e. C ) $.
    vtocl3gf.1 $e |- ( ps -> A. x ps ) $.
    vtocl3gf.2 $e |- ( ch -> A. y ch ) $.
    vtocl3gf.3 $e |- ( th -> A. z th ) $.
    vtocl3gf.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3gf.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3gf.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3gf.7 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl3gf $p |- ( ( A e. V /\ B e. W /\ C e. X ) -> th ) $=
      ( wcel cvv wa elisset wi cv ax-17 hbel hbim wceq imbi2d vtoclgf vtocl2gf
      impcom sylan 3impb ) ILUHZJMUHZKNUHZDVDIUIUHZVEVFUJZDILUKVHVGDVGBULVGCULV
      GDULFGHJKMNRSTVGCFFHHIUIPHUMUIUHZFUNUOUBUPVGDGGHHIUIQVIGUNUOUCUPFUMJUQBCV
      GUEURGUMKUQCDVGUFURABEHIUIOUAUDUGUSUTVAVBVC $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d x A $.  $d y z A $.  $d y z B $.  $d z x $.
    vtocl2gfOLD.1 $e |- ( ps -> A. x ps ) $.
    vtocl2gfOLD.2 $e |- ( ch -> A. y ch ) $.
    vtocl2gfOLD.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gfOLD.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gfOLD.5 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl2gfOLD $p |- ( ( A e. V /\ B e. W ) -> ch ) $=
      ( vz wcel cvv elisset wi cv ax-17 hbim wceq imbi2d vtoclgf impcom sylan
      ) FHPFQPZGIPZCFHRUIUHCUHBSUHCSEOGIOTZGPEUAUHCEUHEUAKUBETGUCBCUHMUDABDOFQU
      JFPDUAJLNUEUEUFUG $.
      $( [25-Apr-1995] $)
  $}

  ${
    $d w x A $.  $d y A $.  $d z A $.  $d y B $.  $d z B $.  $d z C $.
    vtocl3gfOLD.1 $e |- ( ps -> A. x ps ) $.
    vtocl3gfOLD.2 $e |- ( ch -> A. y ch ) $.
    vtocl3gfOLD.3 $e |- ( th -> A. z th ) $.
    vtocl3gfOLD.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3gfOLD.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3gfOLD.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3gfOLD.7 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl3gfOLD $p |- ( ( A e. V /\ B e. W /\ C e. X ) -> th ) $=
      ( vw wcel cvv wa elisset wi ax-17 hbim cv wceq imbi2d vtoclgf
      vtocl2gfOLD impcom sylan 3impb ) HKUBZILUBZJMUBZDUQHUCUBZURUSUDZDHKUEVAUT
      DUTBUFUTCUFUTDUFFGIJLMUTCFUTFUGOUHUTDGUTGUGPUHFUIIUJBCUTRUKGUIJUJCDUTSUKA
      BEUAHUCUAUIHUBEUGNQTULUMUNUOUP $.
      $( [9-Aug-2013] $)
  $}

  ${
    $d w x A $.  $d y A $.  $d w y B $.  $d x ps $.  $d y ch $.
    vtocl2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2g.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2g.3 $e |- ph $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2g $p |- ( ( A e. V /\ B e. W ) -> ch ) $=
      ( vw cv wcel ax-17 vtocl2gf ) ABCDEMFGHIMNZFOZDPSEPRGOEPBDPCEPJKLQ $.
      $( [25-Apr-1995] $)
  $}

  ${
    $d y A $.  $d x B z $.
    vtoclgaf.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgaf.2 $e |- ( ps -> A. x ps ) $.
    vtoclgaf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgaf.4 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       8-Jun-2011.) $)
    vtoclgaf $p |- ( A e. B -> ps ) $=
      ( vz wcel cv wi ax-17 hbel hbim wceq eleq1 imbi12d vtoclgf pm2.43i ) EFLZ
      BCMZFLZANUCBNCDEFGUCBCCDKEFGKMFLCOPHQUDERUEUCABUDEFSITJUAUB $.
      $( [11-Jul-2011] $) $( [17-Feb-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ps $.
    vtoclga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclga.2 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclga $p |- ( A e. B -> ps ) $=
      ( vy cv wcel ax-17 vtoclgaf ) ABCHDEHIDJCKBCKFGL $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d z A $.  $d z B $.  $d x y z C $.  $d x y z D $.
    vtocl2gaf.a $e |- ( z e. A -> A. x z e. A ) $.
    vtocl2gaf.b $e |- ( z e. A -> A. y z e. A ) $.
    vtocl2gaf.c $e |- ( z e. B -> A. y z e. B ) $.
    vtocl2gaf.1 $e |- ( ps -> A. x ps ) $.
    vtocl2gaf.2 $e |- ( ch -> A. y ch ) $.
    vtocl2gaf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gaf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gaf.5 $e |- ( ( x e. C /\ y e. D ) -> ph ) $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2gaf $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( wcel wa cv wi ax-17 hbel hban hbim wceq eleq1 anbi1d imbi12d anbi2d
      vtocl2gf pm2.43i ) GISZHJSZTZCDUAZISZEUAZJSZTZAUBUNUTTZBUBUPCUBDEFGHIJKLM
      VBBDUNUTDDFFGIKFUAZISZDUCUDDFFUSJVCUSSDUCVCJSZDUCUDUENUFUPCEUNUOEEFFGILVD
      EUCUDEFFHJMVEEUCUDUEOUFUQGUGZVAVBABVFURUNUTUQGIUHUIPUJUSHUGZVBUPBCVGUTUOU
      NUSHJUHUKQUJRULUM $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d w x y A $.  $d w y B $.  $d w x y C $.  $d w x y D $.  $d x ps $.
    $d y ch $.
    vtocl2ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2ga.3 $e |- ( ( x e. C /\ y e. D ) -> ph ) $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2ga $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( vw cv wcel ax-17 vtocl2gaf ) ABCDEMFGHIMNZFOZDPSEPRGOEPBDPCEPJKLQ $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d w A $.  $d w B $.  $d w C $.  $d w x y z R $.  $d w x y z S $.
    $d w x y z T $.
    vtocl3gaf.a $e |- ( w e. A -> A. x w e. A ) $.
    vtocl3gaf.b $e |- ( w e. A -> A. y w e. A ) $.
    vtocl3gaf.c $e |- ( w e. A -> A. z w e. A ) $.
    vtocl3gaf.d $e |- ( w e. B -> A. y w e. B ) $.
    vtocl3gaf.e $e |- ( w e. B -> A. z w e. B ) $.
    vtocl3gaf.f $e |- ( w e. C -> A. z w e. C ) $.
    vtocl3gaf.1 $e |- ( ps -> A. x ps ) $.
    vtocl3gaf.2 $e |- ( ch -> A. y ch ) $.
    vtocl3gaf.3 $e |- ( th -> A. z th ) $.
    vtocl3gaf.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3gaf.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3gaf.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3gaf.7 $e |- ( ( x e. R /\ y e. S /\ z e. T ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3gaf $p |- ( ( A e. R /\ B e. S /\ C e. T ) -> th ) $=
      ( wcel w3a cv wi ax-17 hbel hb3an hbim wceq eleq1 3anbi1d imbi12d
      3anbi2d 3anbi3d vtocl3gf pm2.43i ) ILUHZJMUHZKNUHZUIZDEUJZLUHZFUJZMUHZGUJ
      ZNUHZUIZAUKVDVKVMUIZBUKVDVEVMUIZCUKVGDUKEFGHIJKLMNOPQRSTVOBEVDVKVMEEHHILO
      HUJZLUHZEULUMEHHVJMVQVJUHEULVQMUHZEULUMEHHVLNVQVLUHZEULVQNUHZEULUMUNUAUOV
      PCFVDVEVMFFHHILPVRFULUMFHHJMRVSFULUMFHHVLNVTFULWAFULUMUNUBUOVGDGVDVEVFGGH
      HILQVRGULUMGHHJMSVSGULUMGHHKNTWAGULUMUNUCUOVHIUPZVNVOABWBVIVDVKVMVHILUQUR
      UDUSVJJUPZVOVPBCWCVKVEVDVMVJJMUQUTUEUSVLKUPZVPVGCDWDVMVFVDVEVLKNUQVAUFUSU
      GVBVC $.
      $( [10-Aug-2013] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.  $d x y z R $.
    $d x y z S $.
    vtocl3gafOLD.1 $e |- ( ps -> A. x ps ) $.
    vtocl3gafOLD.2 $e |- ( ch -> A. y ch ) $.
    vtocl3gafOLD.3 $e |- ( th -> A. z th ) $.
    vtocl3gafOLD.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3gafOLD.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3gafOLD.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3gafOLD.7 $e |- ( ( x e. D /\ y e. R /\ z e. S ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3gafOLD $p |- ( ( A e. D /\ B e. R /\ C e. S ) -> th ) $=
      ( wcel w3a cv wi ax-17 hbim wceq eleq1 3anbi1d imbi12d 3anbi2d 3anbi3d
      vtocl3gfOLD pm2.43i ) HKUAZILUAZJMUAZUBZDEUCZKUAZFUCZLUAZGUCZMUAZUBZAUDUO
      VBVDUBZBUDUOUPVDUBZCUDURDUDEFGHIJKLMVFBEVFEUENUFVGCFVGFUEOUFURDGURGUEPUFU
      SHUGZVEVFABVHUTUOVBVDUSHKUHUIQUJVAIUGZVFVGBCVIVBUPUOVDVAILUHUKRUJVCJUGZVG
      URCDVJVDUQUOUPVCJMUHULSUJTUMUN $.
      $( [9-Aug-2013] $)
  $}

  ${
    $d w x y z A $.  $d w y z B $.  $d w z C $.  $d w x y z D $.
    $d w x y z R $.  $d w x y z S $.  $d x ps $.  $d y ch $.  $d z th $.
    vtocl3ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3ga.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3ga.4 $e |- ( ( x e. D /\ y e. R /\ z e. S ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3ga $p |- ( ( A e. D /\ B e. R /\ C e. S ) -> th ) $=
      ( vw cv wcel ax-17 vtocl3gaf ) ABCDEFGRHIJKLMRSZHTZEUAUDFUAUDGUAUCITZFUAU
      EGUAUCJTGUABEUACFUADGUANOPQUB $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocleg.1 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocleg $p |- ( A e. V -> ph ) $=
      ( wcel cv wceq wex elex 19.23aiv syl ) CDFBGCHZBIABCDJMABEKL $.
      $( [10-Jan-2004] $)
  $}

  ${
    $d x A $.
    $( Implicit substitution of a class for a set variable.  (Closed theorem
       version of ~ vtoclef .)  (The proof was shortened by Andrew Salmon,
       8-Jun-2011.) $)
    vtoclegft $p |- ( ( A e. B /\ A. x ( ph -> A. x ph ) /\
                   A. x ( x = A -> ph ) ) -> ph ) $=
      ( wcel wal wi cv wceq wex elex exim pm2.27 19.9t syl9 syl7 syl 3imp ) CDE
      ZAABFGBFZBHCIZAGBFZASUABJZTUBAGGBCDKUBUCABJZGZUCTAUAABLUCUEUDTAUCUDMABNOP
      QR $.
      $( [8-Jun-2011] $) $( [7-Nov-2005] $)
  $}

  ${
    $d x A $.
    vtoclef.1 $e |- ( ph -> A. x ph ) $.
    vtoclef.2 $e |- A e. _V $.
    vtoclef.3 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclef $p |- ph $=
      ( cv wceq wex isseti 19.23ai ax-mp ) BGCHZBIABCEJMABDFKL $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocle.1 $e |- A e. _V $.
    vtocle.2 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocle $p |- ph $=
      ( cvv wcel vtocleg ax-mp ) CFGADABCFEHI $.
      $( [9-Sep-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclri.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclri.2 $e |- A. x e. B ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclri $p |- ( A e. B -> ps ) $=
      ( rspec vtoclga ) ABCDEFACEGHI $.
      $( [21-Nov-1994] $)
  $}

  ${
    $d x y $.  $d y A $.
    cla4gft.1 $e |- ( ps -> A. x ps ) $.
    cla4gft.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( A closed version of ~ cla4gf .  (Contributed by Andrew Salmon,
       6-Jun-2011.) $)
    cla4gft $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                      ( A. x ph -> ps ) ) ) $=
      ( wcel cvv cv wceq wb wi wal elisset wex issetf hba1 bi1 imim2i a4s
      eximd syl5bi 19.36 syl6ib syl5 ) EFIEJIZCKELZABMZNZCOZACOBNZEFPULUHABNZCQ
      ZUMUHUICQULUOCDEHRULUIUNCUKCSUKUIUNNCUJUNUIABTUAUBUCUDABCGUEUFUG $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d y A z $.  $d x z $.
    cla4gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    cla4gf.2 $e |- ( ps -> A. x ps ) $.
    cla4gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition.  Compare Theorem
       7.3 of [Quine] p. 44.  (Unnecessary distinct variable restrictions were
       removed by Andrew Salmon, 12-Aug-2011.) $)
    cla4gf $p |- ( A e. V -> ( A. x ph -> ps ) ) $=
      ( vz wcel cvv wal wi elisset wex cv wceq isset hblem hbeleq ax-17 eqeq1
      cbvex bitri biimpd eximi sylbi 19.36 sylib syl ) EFKELKZACMBNZEFOULABNZCP
      ZUMULCQZERZCPZUOULJQZERZJPURJESUTUQJCCJECDJEGTUAUQJUBUSUPEUCUDUEUQUNCUQAB
      IUFUGUHABCHUIUJUK $.
      $( [12-Aug-2011] $) $( [2-Feb-1997] $)

    $d y A $.
    $( Existential specialization, using implicit substitition. $)
    cla4egf $p |- ( A e. V -> ( ps -> E. x ph ) ) $=
      ( wcel wn wal wex hbn cv wceq notbid cla4gf con2d df-ex syl6ibr ) EFJZBAK
      ZCLZKACMUBUDBUCBKCDEFGBCHNCOEPABIQRSACTUA $.
      $( [12-Aug-2011] $) $( [2-Feb-1997] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    cla4gv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition.  Compare Theorem
       7.3 of [Quine] p. 44. $)
    cla4gv $p |- ( A e. V -> ( A. x ph -> ps ) ) $=
      ( vy cv wcel ax-17 cla4gf ) ABCGDEGHDICJBCJFK $.
      $( [22-Jun-1994] $)

    $( Existential specialization, using implicit substitition. $)
    cla4egv $p |- ( A e. V -> ( ps -> E. x ph ) ) $=
      ( vy cv wcel ax-17 cla4egf ) ABCGDEGHDICJBCJFK $.
      $( [14-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla42egv.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with 2 quantifiers, using implicit
       substitution. $)
    cla42egv $p |- ( ( A e. V /\ B e. W ) -> ( ps -> E. x E. y ph ) ) $=
      ( wcel wa cv wceq wex elex anim12i eeanv sylibr biimprcd 2eximdv syl5com
      ) EGJZFHJZKZCLEMZDLFMZKZDNCNZBADNCNUDUECNZUFDNZKUHUBUIUCUJCEGODFHOPUEUFCD
      QRBUGACDUGABISTUA $.
      $( [3-Aug-1995] $)

    $( Specialization with 2 quantifiers, using implicit substitution. $)
    cla42gv $p |- ( ( A e. V /\ B e. W ) -> ( A. x A. y ph -> ps ) ) $=
      ( wcel wa wal wn wex cv wceq notbid cla42egv exnal exbii bitr2i syl6ibr
      con4d ) EGJFHJKZBADLZCLZUDBMZAMZDNZCNZUFMZUHUGCDEFGHCOEPDOFPKABIQRUJUEMZC
      NUKUIULCADSTUECSUAUBUC $.
      $( [27-Apr-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    cla43egv.1 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with 3 quantifiers, using implicit
       substitution. $)
    cla43egv $p |- ( ( A e. V /\ B e. W /\ C e. X ) ->
               ( ps -> E. x E. y E. z ph ) ) $=
      ( wcel w3a cv wceq wex elex 3anim123i eeeanv sylibr biimprcd eximdv
      2eximdv syl5com ) FIMZGJMZHKMZNZCOFPZDOGPZEOHPZNZEQZDQCQZBAEQZDQCQUIUJCQZ
      UKDQZULEQZNUOUFUQUGURUHUSCFIRDGJREHKRSUJUKULCDETUABUNUPCDBUMAEUMABLUBUCUD
      UE $.
      $( [12-May-2008] $)

    $( Specialization with 3 quantifiers, using implicit substitution. $)
    cla43gv $p |- ( ( A e. V /\ B e. W /\ C e. X ) ->
               ( A. x A. y A. z ph -> ps ) ) $=
      ( wcel w3a wal wn wex cv wceq notbid cla43egv exnal exbii bitri bitr2i
      syl6ibr con4d ) FIMGJMHKMNZBAEOZDOZCOZUHBPZAPZEQZDQZCQZUKPZUMULCDEFGHIJKC
      RFSDRGSERHSNABLTUAUPUJPZCQUQUOURCUOUIPZDQURUNUSDAEUBUCUIDUBUDUCUJCUBUEUFU
      G $.
      $( [12-May-2008] $)
  $}

  ${
    $d x A $.  $d x ps $.
    cla4v.1 $e |- A e. _V $.
    cla4v.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition. $)
    cla4v $p |- ( A. x ph -> ps ) $=
      ( cvv wcel wal wi cla4gv ax-mp ) DGHACIBJEABCDGFKL $.
      $( [22-Jun-1994] $)

    $( Existential specialization, using implicit substitition.  (The proof was
       shortened by Eric Schmidt, 22-Dec-2006.) $)
    cla4ev $p |- ( ps -> E. x ph ) $=
      ( cvv wcel wex wi cla4egv ax-mp ) DGHBACIJEABCDGFKL $.
      $( [22-Dec-2006] $) $( [31-Dec-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2v.1 $e |- A e. _V $.
    cla4e2v.2 $e |- B e. _V $.
    cla4e2v.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization, using implicit substitition. $)
    cla42ev $p |- ( ps -> E. x E. y ph ) $=
      ( cvv wcel wex wi cla42egv mp2an ) EJKFJKBADLCLMGHABCDEFJJINO $.
      $( [3-Aug-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d A z $.  $d x z $.
    rcla4t.1 $e |- ( ps -> A. x ps ) $.
    $( A closed version of ~ rcla4 .  (Contributed by Andrew Salmon,
       6-Jun-2011.) $)
    rcla4t $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                   ( A. x e. B ph -> ps ) ) ) $=
      ( vz cv wceq wb wi wal wcel wral df-ral wa eleq1 adantr simpr imbi12d ex
      a2i alimi ax-17 hbim cla4gft syl syl7bi com34 pm2.43d ) CHZDIZABJZKZCLZDE
      MZACENZBKUOUPUQUPBUQUKEMZAKZCLZUOUPUPBKZACEOUOULUSVAJZKZCLUPUTVAKKUNVCCUL
      UMVBULUMVBULUMPURUPABULURUPJUMUKDEQRULUMSTUAUBUCUSVACGDEUPBCUPCUDFUEGHDMC
      UDUFUGUHUIUJ $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x y A $.  $d x B $.
    rcla4.1 $e |- ( ps -> A. x ps ) $.
    rcla4.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization, using implicit substitition.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    rcla4 $p |- ( A e. B -> ( A. x e. B ph -> ps ) ) $=
      ( vy wral cv wcel wi wal df-ral ax-17 hbim wceq eleq1 imbi12d cla4gf
      pm2.43a syl5bi ) ACEICJZEKZALZCMZDEKZBACENUFUGBUEUGBLCHDEHJDKCOUGBCUGCOFP
      UCDQUDUGABUCDERGSTUAUB $.
      $( [8-Jun-2011] $) $( [19-Apr-2005] $)

    $( Restricted existential specialization, using implicit substitition. $)
    rcla4e $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( vy wcel wa cv wex wrex ax-17 hban wceq eleq1 anbi12d cla4egf anabsi5
      df-rex sylibr ) DEIZBJZCKZEIZAJZCLZACEMUCBUHUGUDCHDEHKDICNUCBCUCCNFOUEDPU
      FUCABUEDEQGRSTACEUAUB $.
      $( [26-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rcla4v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization, using implicit substitition. $)
    rcla4v $p |- ( A e. B -> ( A. x e. B ph -> ps ) ) $=
      ( ax-17 rcla4 ) ABCDEBCGFH $.
      $( [24-May-2005] $) $( [26-May-1998] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4cv $p |- ( A. x e. B ph -> ( A e. B -> ps ) ) $=
      ( wcel wral rcla4v com12 ) DEGACEHBABCDEFIJ $.
      $( [2-Feb-2006] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4va $p |- ( ( A e. B /\ A. x e. B ph ) -> ps ) $=
      ( wcel wral rcla4v imp ) DEGACEHBABCDEFIJ $.
      $( [13-Sep-2005] $)

    $( Restricted specialization, using implicit substitition.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    rcla4cva $p |- ( ( A. x e. B ph /\ A e. B ) -> ps ) $=
      ( wcel wral rcla4v impcom ) DEGACEHBABCDEFIJ $.
      $( [8-Jun-2011] $) $( [26-Jul-2006] $)

    $( Restricted existential specialization, using implicit substitition. $)
    rcla4ev $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( ax-17 rcla4e ) ABCDEBCGFH $.
      $( [26-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.  $d x ch $.
    rcla4dv.1 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Restricted specialization, using implicit substitition. $)
    rcla4dv $p |- ( ( ph /\ A e. B ) -> ( A. x e. B ps -> ch ) ) $=
      ( wcel wral wi r19.21v cv wceq wb expcom pm5.74d rcla4v syl5bir pm2.86d
      impcom ) EFHZABDFIZCJUAAUBCAUBJABJZDFIUAACJZABDFKUCUDDEFDLEMZABCAUEBCNGOP
      QRST $.
      $( [17-Feb-2007] $)

    $( Restricted existential specialization, using implicit substitition.
       (Contributed by FL, 17-Apr-2007.) $)
    rcla4edv $p |- ( ( ph /\ A e. B ) -> ( ch -> E. x e. B ps ) ) $=
      ( wcel wrex wi wa cv wceq wb expcom pm5.74d rcla4ev r19.37av syl ex
      pm2.86d impcom ) EFHZACBDFIZJUCACUDUCACJZAUDJZUCUEKABJZDFIUFUGUEDEFDLEMZA
      BCAUHBCNGOPQABDFRSTUAUB $.
      $( [10-May-2008] $) $( [17-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x C $.  $d x y D $.
    rcla42.1 $e |- ( ch -> A. x ch ) $.
    rcla42.2 $e |- ( ps -> A. y ps ) $.
    rcla42.3 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla42.4 $e |- ( y = B -> ( ch <-> ps ) ) $.
    $( 2-variable restricted specialization, using implicit substitition. $)
    rcla42 $p |- ( ( A e. C /\ B e. D ) -> ( A. x e. C A. y e. D ph ->
                  ps ) ) $=
      ( wcel wral cv ax-17 hbral wceq ralbidv rcla4 sylan9 ) FHNAEIOZDHOCEIOZGI
      NBUCUDDFHCDEIEPINDQJRDPFSACEILTUACBEGIKMUAUB $.
      $( [9-Nov-2012] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x C $.  $d x y D $.  $d x ch $.  $d y ps $.
    rcla42v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla42v.2 $e |- ( y = B -> ( ch <-> ps ) ) $.
    $( 2-variable restricted specialization, using implicit substitition. $)
    rcla42v $p |- ( ( A e. C /\ B e. D ) -> ( A. x e. C A. y e. D ph ->
                  ps ) ) $=
      ( ax-17 rcla42 ) ABCDEFGHICDLBELJKM $.
      $( [24-May-2005] $) $( [13-Sep-1999] $)

    $( 2-variable restricted existential specialization, using implicit
       substitution. $)
    rcla42ev $p |- ( ( A e. C /\ B e. D /\ ps ) -> E. x e. C E. y e. D ph ) $=
      ( wcel w3a wrex wa rcla4ev anim2i 3impb cv wceq rexbidv syl ) FHLZGILZBMU
      CCEINZOZAEINZDHNUCUDBUFUDBOUEUCCBEGIKPQRUGUEDFHDSFTACEIJUAPUB $.
      $( [13-Feb-2007] $) $( [16-Oct-1999] $)
  $}

  ${
    $d z ps $.  $d x ch $.  $d y th $.  $d x y z A $.  $d y z B $.  $d z C $.
    $d x R $.  $d x y S $.  $d x y z T $.
    rcla43v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla43v.2 $e |- ( y = B -> ( ch <-> th ) ) $.
    rcla43v.3 $e |- ( z = C -> ( th <-> ps ) ) $.
    $( 3-variable restricted specialization, using implicit substitition. $)
    rcla43v $p |- ( ( A e. R /\ B e. S /\ C e. T ) ->
                  ( A. x e. R A. y e. S A. z e. T ph -> ps ) ) $=
      ( wcel wral wi wa cv wceq ralbidv rcla42v rcla4v sylan9 3impa ) HKQZILQZJ
      MQZAGMRZFLREKRZBSUHUITULDGMRZUJBUKUMCGMREFHIKLEUAHUBACGMNUCFUAIUBCDGMOUCU
      DDBGJMPUEUFUG $.
      $( [10-May-2005] $)

    $( 3-variable restricted existentional specialization, using implicit
       substitition. $)
    rcla43ev $p |- ( ( ( A e. R /\ B e. S /\ C e. T ) /\ ps ) ->
                  E. x e. R E. y e. S E. z e. T ph ) $=
      ( wcel w3a wa wrex simpl1 simpl2 rcla4ev 3ad2antl3 cv wceq rexbidv
      rcla42ev syl111anc ) HKQZILQZJMQZRBSUJUKDGMTZAGMTZFLTEKTUJUKULBUAUJUKULBU
      BULUJBUMUKDBGJMPUCUDUNUMCGMTEFHIKLEUEHUFACGMNUGFUEIUFCDGMOUGUHUI $.
      $( [25-Jul-2012] $)
  $}

  ${
    $d x A y z $.  $d x B y z $.
    eqvinc.1 $e |- A e. _V $.
    $( A variable introduction law for class equality.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    eqvinc $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( wceq cv wa wex wi isseti ax-1 eqtr ex jca eximi ax-mp pm3.43 19.37aiv
      eqtr2 19.23aiv impbii ) BCEZAFZBEZUCCEZGZAHUBUFAUBUDIZUBUEIZGZAHZUBUFIZAH
      UDAHUJABDJUDUIAUDUGUHUDUBKUDUBUEUCBCLMNOPUIUKAUBUDUEQOPRUFUBAUCBCSTUA $.
      $( [8-Jun-2011] $) $( [14-Apr-1995] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    eqvincf.1 $e |- ( y e. A -> A. x y e. A ) $.
    eqvincf.2 $e |- ( y e. B -> A. x y e. B ) $.
    eqvincf.3 $e |- A e. _V $.
    $( A variable introduction law for class equality, using bound-variable
       hypotheses instead of distinct variable conditions. $)
    eqvincf $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( wceq cv wa wex eqvinc hbeleq hban ax-17 eqeq1 anbi12d cbvex bitri ) CDH
      BIZCHZTDHZJZBKAIZCHZUDDHZJZAKBCDGLUCUGBAUAUBAABCEMABDFMNUGBOTUDHUAUEUBUFT
      UDCPTUDDPQRS $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d x A y $.  $d ph y $.
    alexeq.1 $e |- A e. _V $.
    $( Two ways to express substitution of ` A ` for ` x ` in ` ph ` . $)
    alexeq $p |- ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) $=
      ( vy cv wceq wa wex wi wal eqeq2 anbi1d exbidv imbi1d albidv sb56 vtoclb
      bicomi ) BFZCGZAHZBIZUAAJZBKZTEFZGZAHZBIUGAJZBKUCUEECDUFCGZUHUBBUJUGUAAUF
      CTLZMNUJUIUDBUJUGUAAUKOPABEQRS $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x A y $.  $d ph y $.
    $( Equality implies equivalence with substitution. $)
    ceqex $p |- ( x = A -> ( ph <-> E. x ( x = A /\ ph ) ) ) $=
      ( vy cvv wcel cv wceq wa wex wb 19.8a isset sylibr wi eqeq2 anbi1d
      exbidv bibi2d imbi12d ex wal visset alexeq ax-4 com12 syl5bir impbid
      vtoclg mpcom ) CEFZBGZCHZAUMAIZBJZKZUMUMBJUKUMBLBCMNULDGZHZAURAIZBJZKZOUM
      UPODCEUQCHZURUMVAUPUQCULPZVBUTUOAVBUSUNBVBURUMAVCQRSTURAUTURAUTUSBLUAUTUR
      AOZBUBZURAABUQDUCUDVEURAVDBUEUFUGUHUIUJ $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x y A $.
    ceqsexg.1 $e |- ( ps -> A. x ps ) $.
    ceqsexg.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsexg $p |- ( A e. V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( vy wb cv wceq wa wex wcel ax-17 hbe1 hbbi ceqex bibi12d biid vtoclgf )
      AAICJDKZALZCMZBICHDEHJDNCOUDBCUCCPFQUBAUDABACDRGSATUA $.
      $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    ceqsexgv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexgv $p |- ( A e. V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( ax-17 ceqsexg ) ABCDEBCGFH $.
      $( [29-Dec-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    ceqsrexv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of a restricted existential quantifier, using implicit
       substitution. $)
    ceqsrexv $p |- ( A e. B -> ( E. x e. B ( x = A /\ ph ) <-> ps ) ) $=
      ( cv wceq wa wrex wcel wex df-rex an12 exbii bitr4i eleq1 anbi12d
      ceqsexgv bianabs syl5bb ) CGZDHZAIZCEJZUCUBEKZAIZIZCLZDEKZBUEUFUDIZCLUIUD
      CEMUHUKCUCUFANOPUJUIBUGUJBICDEUCUFUJABUBDEQFRSTUA $.
      $( [30-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $d x y D $.  $d x ps $.  $d y ch $.
    ceqsrex2v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsrex2v.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of a restricted existential quantifier, using implicit
       substitution. $)
    ceqsrex2v $p |- ( ( A e. C /\ B e. D ) ->
      ( E. x e. C E. y e. D ( ( x = A /\ y = B ) /\ ph ) <-> ch ) ) $=
      ( wcel cv wceq wa wrex anass rexbii r19.42v bitri anbi2d rexbidv
      ceqsrexv syl5bb sylan9bb ) FHLZDMFNZEMGNZOAOZEIPZDHPZUHBOZEIPZGILCUKUGUHA
      OZEIPZOZDHPUFUMUJUPDHUJUGUNOZEIPUPUIUQEIUGUHAQRUGUNEISTRUOUMDFHUGUNULEIUG
      ABUHJUAUBUCUDBCEGIKUCUE $.
      $( [29-Oct-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel2.1 $e |- A e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel2 $p |- ( A e. B <-> A. x ( x = A -> x e. B ) ) $=
      ( cv wceq wcel wi wal eleq1 ceqsalv bicomi ) AEZBFMCGZHAIBCGZNOABDMBCJKL
      $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3g $p |- ( B e. V -> ( A e. B <-> E. x ( x = B /\ A e. x ) ) ) $=
      ( wcel cv wceq wa wex eleq2 ceqsexgv bicomd ) CDEAFZCGBMEZHAIBCEZNOACDMCB
      JKL $.
      $( [13-Aug-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel3.1 $e |- B e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3 $p |- ( A e. B <-> E. x ( x = B /\ A e. x ) ) $=
      ( cvv wcel cv wceq wa wex wb clel3g ax-mp ) CEFBCFAGZCHBNFIAJKDABCELM $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel4.1 $e |- B e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel4 $p |- ( A e. B <-> A. x ( x = B -> A e. x ) ) $=
      ( cv wceq wcel wi wal eleq2 ceqsalv bicomi ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d y A $.  $d x y $.  $d y ph $.
    $( Restricted quantifier version of Theorem 19.3 of [Margaris] p. 89.  We
       don't need the non-empty class condition of ~ r19.3rzv when there is an
       outer quantifier. $)
    rr19.3v $p |- ( A. x e. A A. y e. A ph <-> A. x e. A ph ) $=
      ( wral cv wceq biidd rcla4v ralimia wcel ax-1 r19.21aiv ralimi impbii )
      ACDEZBDEABDEPABDAACBFZDCFZQGAHIJAPBDAACDARDKLMNO $.
      $( [25-Oct-2012] $)

    $( Restricted quantifier version of Theorem 19.28 of [Margaris] p. 90.  We
       don't need the non-empty class condition of ~ r19.28zv when there is an
       outer quantifier. $)
    rr19.28v $p |- ( A. x e. A A. y e. A ( ph /\ ps )
                      <-> A. x e. A ( ph /\ A. y e. A ps ) ) $=
      ( wa wral cv wcel simpl ralimi wceq biidd rcla4v syl5 wi simpr a1i jcad
      ralimia r19.28av impbii ) ABFZDEGZCEGABDEGZFZCEGUDUFCECHZEIZUDAUEUDADEGUH
      AUCADEABJKAADUGEDHUGLAMNOUDUEPUHUCBDEABQKRSTUFUDCEABDEUAKUB $.
      $( [29-Oct-2012] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.
    $( Membership in a class abstraction, using implicit substitition.  (Closed
       theorem version of ~ elabg .)  (The proof was shortened by Andrew
       Salmon, 8-Jun-2011.) $)
    elabgt $p |- ( ( A e. B /\ A. x ( x = A -> ( ph <-> ps ) ) ) ->
                 ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wceq wb wi wal wcel cab abid eleq1 syl5bbr bibi1d biimpd a2i
      alimi ax-17 hbab1 hbel hbbi pm5.5 cla4gf imp sylan2 ) CGZDHZABIZJZCKDELZU
      JDACMZLZBIZJZCKZUPULUQCUJUKUPUJUKUPUJAUOBAUIUNLUJUOACNUIDUNOPQRSTUMURUPUQ
      UPCFDEFGDLCUAZUOBCCFFDUNUSACFUBUCBCUAUDUJUPUEUFUGUH $.
      $( [8-Jun-2011] $) $( [7-Nov-2005] $)
  $}

  ${
    $d ps y $.  $d x A y $.  $d y ph $.
    elabf.1 $e |- ( ps -> A. x ps ) $.
    elabf.2 $e |- A e. _V $.
    elabf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition. $)
    elabf $p |- ( A e. { x | ph } <-> ps ) $=
      ( vy cab wcel wb cv ax-17 hbab1 hbel hbbi wceq abid eleq1 syl5bbr bitr3d
      vtoclef ) DACIZJZBKCDUDBCCHHDUCHLDJCMACHNOEPFCLZDQZAUDBAUEUCJUFUDACRUEDUC
      STGUAUB $.
      $( [1-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab.1 $e |- A e. _V $.
    elab.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elab $p |- ( A e. { x | ph } <-> ps ) $=
      ( ax-17 elabf ) ABCDBCGEFH $.
      $( [1-Aug-1994] $)
  $}

  ${
    $d y A $.  $d z ph $.  $d x z $.  $d y z $.
    elabgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elabgf.2 $e |- ( ps -> A. x ps ) $.
    elabgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44.  This version has bound-variable
       hypotheses in place of distinct variable restrictions. $)
    elabgf $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vz cv cab wcel wb hbab1 hbel hbbi wceq eleq1 bibi12d abid vtoclgf ) CKZ
      ACLZMZANEUDMZBNCDEFGUFBCCDJEUDGACJOPHQUCERUEUFABUCEUDSITACUAUB $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    elabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elabg $p |- ( A e. V -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wcel ax-17 elabgf ) ABCGDEGHDICJBCJFK $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2g.2 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2g $p |- ( A e. V -> ( A e. B <-> ps ) ) $=
      ( wcel cab eleq2i elabg syl5bb ) DEIDACJZIDFIBENDHKABCDFGLM $.
      $( [12-Sep-2004] $) $( [13-Sep-1995] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2.1 $e |- A e. _V $.
    elab2.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2.3 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2 $p |- ( A e. B <-> ps ) $=
      ( cvv wcel wb elab2g ax-mp ) DIJDEJBKFABCDEIGHLM $.
      $( [12-Sep-2004] $) $( [13-Sep-1995] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab4g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab4g.2 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab4g $p |- ( A e. B <-> ( A e. _V /\ ps ) ) $=
      ( wcel cvv wa elisset pm4.71ri elab2g pm5.32i bitri ) DEHZDIHZPJQBJPQDEKL
      QPBABCDEIFGMNO $.
      $( [17-Oct-2012] $)
  $}

  ${
    $d y A $.
    elab3gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elab3gf.2 $e |- ( ps -> A. x ps ) $.
    elab3gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabgf . $)
    elab3gf $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( wcel cab wb wn elabgf ibi pm2.21 impbid2 ja ) BEFJEACKZJZBLBMTBTBABCDES
      GHINOBTPQABCDEFGHINR $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    elab3g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabg . $)
    elab3g $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wcel ax-17 elab3gf ) ABCGDEGHDICJBCJFK $.
      $( [14-Jun-2011] $) $( [29-Aug-2006] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab3.1 $e |- ( ps -> A e. _V ) $.
    elab3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction using implicit substitution. $)
    elab3 $p |- ( A e. { x | ph } <-> ps ) $=
      ( cvv wcel wi cab wb elab3g ax-mp ) BDGHIDACJHBKEABCDGFLM $.
      $( [10-Nov-2000] $)
  $}

  ${
    $d y A $.  $d y B $.
    elrabf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elrabf.2 $e |- ( y e. B -> A. x y e. B ) $.
    elrabf.3 $e |- ( ps -> A. x ps ) $.
    elrabf.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted class abstraction, using implicit
       substitution.  This version has bound-variable hypotheses in place of
       distinct variable restrictions. $)
    elrabf $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( crab wcel cvv wa elisset adantr cv cab df-rab eleq2i hbel hban wceq
      eleq1 anbi12d elabgf syl5bb pm5.21nii ) EACFKZLZEMLZEFLZBNZEUIOULUKBEFOPU
      JECQZFLZANZCRZLUKUMUIUQEACFSTUPUMCDEMGULBCCDDEFGHUAIUBUNEUCUOULABUNEFUDJU
      EUFUGUH $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $d x y B $.
    elrab.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted class abstraction, using implicit
       substitution. $)
    elrab $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( vy cv wcel ax-17 elrabf ) ABCGDEGHZDICJLEICJBCJFK $.
      $( [21-May-1999] $)

    $( Membership in a restricted class abstraction, using implicit
       substitution. $)
    elrab3 $p |- ( A e. B -> ( A e. { x e. B | ph } <-> ps ) ) $=
      ( crab wcel elrab baib ) DACEGHDEHBABCDEFIJ $.
      $( [5-Oct-2006] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $d x y B $.
    elrab2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elrab2.2 $e |- C = { x e. B | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elrab2 $p |- ( A e. C <-> ( A e. B /\ ps ) ) $=
      ( wcel crab wa eleq2i elrab bitri ) DFIDACEJZIDEIBKFODHLABCDEGMN $.
      $( [2-Nov-2006] $)
  $}

  ${
    $d x y $.  $d ps y $.
    ralab.1 $e |- ( y = x -> ( ph <-> ps ) ) $.
    $( Universal quantification over a class abstraction.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    ralab $p |- ( A. x e. { y | ph } ch <-> A. x ( ps -> ch ) ) $=
      ( cab wral cv wcel wi wal df-ral visset elab imbi1i albii bitri ) CDAEGZH
      DIZSJZCKZDLBCKZDLCDSMUBUCDUABCABETDNFOPQR $.
      $( [20-Oct-2011] $) $( [10-Jun-2010] $)

    $d A x y $.  $d ph x $.
    $( Universal quantification over a restricted class abstraction.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    ralrab $p |- ( A. x e. { y e. A | ph } ch <-> A. x e. A ( ps -> ch ) ) $=
      ( cv wcel wa cab wral wi wal crab wceq eleq1 anbi12d ralab df-rab raleqi
      df-ral impexp albii bitr4i 3bitr4i ) CDEHZFIZAJZEKZLDHZFIZBJZCMZDNZCDAEFO
      ZLBCMZDFLZUIUMCDEUGUKPUHULABUGUKFQGRSCDUPUJAEFTUAURULUQMZDNUOUQDFUBUNUSDU
      LBCUCUDUEUF $.
      $( [20-Oct-2011] $) $( [10-Jun-2010] $)
  $}

  ${
    $d ps x $.  $d A x $.  $d x y $.
    rexrab.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Existential quantification over a class abstraction.  (Contributed by
       Jeff Madsen, 17-Jun-2011.) $)
    rexrab $p |- ( E. y e. { x e. A | ph } ch <-> E. y e. A ( ps /\ ch ) ) $=
      ( wa crab cv wcel elrab anbi1i anass bitri rexbii2 ) CBCHZEADFIZFEJZRKZCH
      SFKZBHZCHUAQHTUBCABDSFGLMUABCNOP $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d x z $.  $d y z $.  $d ph z $.  $d ps z $.
    cbvab.1 $e |- ( ph -> A. y ph ) $.
    cbvab.2 $e |- ( ps -> A. x ps ) $.
    cbvab.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitution.
       (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    cbvab $p |- { x | ph } = { y | ps } $=
      ( vz cab cv wsbc wcel hbsb wceq wb equcoms bicomd sbie sbequ syl5bbr
      df-clab 3bitr4i eqriv ) HACIZBDIZACHJZKBDUFKZUFUDLUFUELAUGCHBDHCFMABDCJZK
      UHUFNUGBADCEDJUHNABABOCDGPQRBCHDSTRAHCUABHDUAUBUC $.
      $( [11-Jul-2011] $) $( [10-Jul-2011] $)
  $}

  ${
    $d y ph $.  $d x ps $.
    cbvabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitution. $)
    cbvabv $p |- { x | ph } = { y | ps } $=
      ( ax-17 cbvab ) ABCDADFBCFEG $.
      $( [11-Jul-2011] $) $( [26-May-1999] $)
  $}

  ${
    $d A w z $.  $d y w $.  $d x w $.  $d ph w $.  $d ps w $.
    cbvrab.1 $e |- ( z e. A -> A. x z e. A ) $.
    cbvrab.2 $e |- ( z e. A -> A. y z e. A ) $.
    cbvrab.3 $e |- ( ph -> A. y ph ) $.
    cbvrab.4 $e |- ( ps -> A. x ps ) $.
    cbvrab.5 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted class abstraction,
       using implicit substitution.  This version has bound-variable hypotheses
       in place of distinct variable conditions.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    cbvrab $p |- { x e. A | ph } = { y e. A | ps } $=
      ( vw cv wcel wa cab crab wsbc ax-17 hblem hbs1 hban wceq eleq1 sbequ12
      anbi12d cbvab hbsb sbequ sbie syl6bb eqtri df-rab 3eqtr4i ) CMZFNZAOZCPZD
      MZFNZBOZDPZACFQBDFQURLMZFNZACVCRZOZLPVBUQVFCLUQLSVDVECCELFGTACLUAUBUOVCUC
      UPVDAVEUOVCFUDACLUEUFUGVFVALDVDVEDDELFHTACLDIUHUBVALSVCUSUCZVDUTVEBVCUSFU
      DVGVEACUSRBALDCUIABCDJKUJUKUFUGULACFUMBDFUMUN $.
      $( [11-Jul-2011] $) $( [26-Sep-2003] $)
  $}

  ${
    $d x y z A $.  $d y ph $.  $d x ps $.
    cbvrabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted class abstraction,
       using implicit substitution. $)
    cbvrabv $p |- { x e. A | ph } = { y e. A | ps } $=
      ( vz cv wcel ax-17 cbvrab ) ABCDGEGHEIZCJLDJADJBCJFK $.
      $( [11-Jul-2011] $) $( [26-May-1999] $)
  $}

  ${
    $d w y A $.  $d w x z $.  $d x y $.  $d A z $.
    $( Identity used to create closed-form versions of bound-variable
       hypothesis builders for class expressions.  (Unnecessary distinct
       variable restrictions were removed by Andrew Salmon, 11-Jul-2011.) $)
    abidhb $p |- ( A. y ( y e. A -> A. x y e. A ) ->
                 { z | A. x z e. A } = A ) $=
      ( vw cv wcel wal wi cab ax-17 wceq eleq1 albidv cbvab hba1 ax-4 impbid2
      abbid cbvabv abid2 3eqtr3g syl5eq ) BFZDGZUEAHZIZBHZCFZDGZAHZCJEFZDGZAHZE
      JZDUKUNCEUKEKUNCKUIULLUJUMAUIULDMNOUHUFBJUEBJUODUHUFUEBUGBPUHUFUEUEAQUGBQ
      RSUFUNBEUDULLUEUMAUDULDMNTBDUAUBUC $.
      $( [11-Jul-2011] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y w A $.  $d z w B $.  $d y ph $.  $d z ph $.  $d w x y $.  $d x z $.
    hbeqd.1 $e |- ( ph -> A. x ph ) $.
    hbeqd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbeqd.3 $e |- ( ph -> ( z e. B -> A. x z e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbeq . $)
    hbeqd $p |- ( ph -> ( A = B -> A. x A = B ) ) $=
      ( vw cv wcel wal cab wceq wi hba1 hbab hbeq a1i 19.21aiv abidhb syl
      eqeq12d albid 3imtr3d ) AJKZELZBMZJNZUGFLZBMZJNZOZUNBMZEFOZUPBMUNUOPABCDU
      JUMUIBJCUHBQRULBJDUKBQRSTAUJEUMFACKELZUQBMPZCMUJEOAURCHUABCJEUBUCADKFLZUS
      BMPZDMUMFOAUTDIUABDJFUBUCUDZAUNUPBGVAUEUF $.
      $( [17-Feb-2013] $) $( [6-Dec-2005] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y ph $.  $d x y z $.
    hbeqdOLD.1 $e |- ( ph -> A. x ph ) $.
    hbeqdOLD.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbeqdOLD.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbeq . $)
    hbeqdOLD $p |- ( ph -> ( A = B -> A. x A = B ) ) $=
      ( vz cv wcel wal cab wceq wi hba1 hbab hbeq a1i 19.21aiv abidhb syl
      eqeq12d albid 3imtr3d ) AIJZDKZBLZIMZUFEKZBLZIMZNZUMBLZDENZUOBLUMUNOABCCU
      IULUHBICUGBPQUKBICUJBPQRSAUIDULEACJZDKZUQBLOZCLUIDNAURCGTBCIDUAUBAUPEKZUS
      BLOZCLULENAUTCHTBCIEUAUBUCZAUMUOBFVAUDUE $.
      $( [6-Dec-2005] $)
  $}

  ${
    $d y w A $.  $d z w B $.  $d y ph $.  $d z ph $.  $d w x y $.  $d x z $.
    hbned.1 $e |- ( ph -> A. x ph ) $.
    hbned.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbned.3 $e |- ( ph -> ( z e. B -> A. x z e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbne . $)
    hbned $p |- ( ph -> ( A =/= B -> A. x A =/= B ) ) $=
      ( wceq wn wal wne hbeqd hbnd df-ne albii 3imtr4g ) AEFJZKZTBLEFMZUABLASBG
      ABCDEFGHINOEFPZUATBUBQR $.
      $( [18-Mar-2013] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y ph $.  $d x y z $.
    hbeld.1 $e |- ( ph -> A. x ph ) $.
    hbeld.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbeld.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbel . $)
    hbeld $p |- ( ph -> ( A e. B -> A. x A e. B ) ) $=
      ( vz cv wcel wal cab wi hba1 hbab hbel a1i wceq 19.21aiv abidhb syl
      eleq12d albid 3imtr3d ) AIJZDKZBLZIMZUFEKZBLZIMZKZUMBLZDEKZUOBLUMUNNABCCU
      IULUHBICUGBOPUKBICUJBOPQRAUIDULEACJZDKZUQBLNZCLUIDSAURCGTBCIDUAUBAUPEKZUS
      BLNZCLULESAUTCHTBCIEUAUBUCZAUMUOBFVAUDUE $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d x y z $.  $d y A $.  $d y z ph $.
    hbeleqd.1 $e |- ( ph -> A. x ph ) $.
    hbeleqd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbeleq . $)
    hbeleqd $p |- ( ph -> ( y = A -> A. x y = A ) ) $=
      ( vz cv wcel a17d hbeqd ) ABGCCHZDEAGHLIBJFK $.
      $( [23-Feb-2013] $)
  $}

  ${
    $d y A $.  $d x z $.  $d x y $.  $d z A $.
    dedhb.1 $e |- ( A = { z | A. x z e. A } -> ( ph <-> ps ) ) $.
    dedhb.2 $e |- ps $.
    $( A deduction theorem for converting the inference
       ` |- ( y e. A -> A. x y e. A ) ` => ` |- ph ` into a closed theorem.
       Use ~ hba1 and ~ hbab to eliminate the hypothesis of the substitution
       instance ` ps ` of the inference.  For converting the inference form
       into a deduction form, ~ abidhb is useful. $)
    dedhb $p |- ( A. y ( y e. A -> A. x y e. A ) -> ph ) $=
      ( cv wcel wal wi cab wceq wb abidhb eqcomd syl mpbiri ) DIFJZTCKLDKZABHUA
      FEIFJCKEMZNABOUAUBFCDEFPQGRS $.
      $( [11-Jul-2011] $) $( [8-Dec-2006] $)
  $}

  ${
    $d y z A $.  $d x z $.  $d y z $.
    $( Closed theorem form of ~ vtoclgf . $)
    vtoclgft $p |- ( ( ( A. x A. y ( y e. A -> A. x y e. A )
                     /\ A. x ( ps -> A. x ps ) )
                  /\ ( A. x ( x = A -> ( ph <-> ps ) )
                     /\ A. x ph ) /\ A e. V ) -> ps ) $=
      ( vz wcel cv wal wi wa wceq wb cvv elisset w3a wex elex 3ad2ant3 hba1
      hba2 ax-4 19.21bi hblemd hbeleqd eqeq1 a1i cbvexd ad2antrr 3adant3 mpbid
      bi1 imim2i com23 imp alanimi 3ad2ant2 simp1r 19.23t syl mpd syl3an3 ) EFH
      DIEHZVDCJKZDJZCJZBBCJKCJZLZCIZEMZABNZKZCJACJLZEOHZBEFPVIVNVOQZVKCRZBVPGIZ
      EMZGRZVQVOVIVTVNGEOSTVIVNVTVQNZVOVGWAVHVNVGVSVKGCVFCUAZVGCGEWBVGCDGEWBVED
      CUBVGVEDVFCUCUDUEUFVRVJMVSVKNKVGVRVJEUGUHUIUJUKULVPVKBKZCJZVQBKZVNVIWDVOV
      MAWCCVMAWCVMVKABVLABKVKABUMUNUOUPUQURVPVHWDWENVGVHVNVOUSVKBCUTVAULVBVC $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d x y A $.
    $( Equality has existential uniqueness. $)
    eueq $p |- ( A e. _V <-> E! x x = A ) $=
      ( vy cv wceq wex wa wi wal cvv wcel weu eqtr3 gen2 biantru isset eqeq1
      eu4 3bitr4i ) ADZBEZAFZUBUACDZBEZGTUCEHZCIAIZGBJKUAALUFUBUEACTUCBMNOABPUA
      UDACTUCBQRS $.
      $( [25-Nov-1994] $)
  $}

  ${
    $d x A $.
    eueq1.1 $e |- A e. _V $.
    $( Equality has existential uniqueness. $)
    eueq1 $p |- E! x x = A $=
      ( cvv wcel cv wceq weu eueq mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    eueq2.1 $e |- A e. _V $.
    eueq2.2 $e |- B e. _V $.
    $( Equality has existential uniqueness (split into 2 cases). $)
    eueq2 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ph /\ x = B ) ) $=
      ( cv wceq wa wn wo weu notnot1 eueq1 euanv biimpri mpan2 euorv syl11anc
      orcom bianfd orbi2d syl5bb eubidv mpbid mpdan id orbi1d pm2.61i ) AABGZCH
      ZIZAJZUJDHZIZKZBLZAUMULKZBLZUQAUMJULBLZUSAMZAUKBLZUTBCENUTAVBIAUKBOPQUMUL
      BRSAURUPBURULUMKAUPUMULTAUMUOULAUMUNVAUAUBUCUDUEUMAUOKZBLZUQUMUOBLZVDUMUN
      BLZVEBDFNVEUMVFIUMUNBOPQAUOBRUFUMVCUPBUMAULUOUMAUKUMUGUAUHUDUEUI $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x ph $.  $d x ps $.  $d x A $.  $d x B $.  $d x C $.
    eueq3.1 $e |- A e. _V $.
    eueq3.2 $e |- B e. _V $.
    eueq3.3 $e |- C e. _V $.
    eueq3.4 $e |- -. ( ph /\ ps ) $.
    $( Equality has existential uniqueness (split into 3 cases). $)
    eueq3 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( cv wceq wa wo wn w3o weu pm2.45 wi imnan mpbir con2i jaoi eueq1 euanv
      biimpri mpan2 euorv syl11anc notnot1 orcs bianfd orbi12d orbi2d orcom
      3orass 3bitr4g eubidv mpbid pm2.46 olcs orbi1d df-3or syl6bbr mpdan
      syl6bb orass ecase3 ) ABACKZDLZMZABNZOZVIELZMZBVIFLZMZPZCQZAVMBNZVKNZCQZV
      SAVTOVKCQZWBVTAVMAOBABRZABABOZSABMOJABTUAZUBZUCUBAVJCQZWCCDGUDWCAWHMAVJCU
      EUFUGVTVKCUHUIAWAVRCAVKVTNVKVOVQNZNZWAVRAVTWIVKAVMVOBVQAVMVNABVMOZVLUJZUK
      ULABVPWFULUMUNVTVKUOVKVOVQUPZUQURUSBAVMNZVQNZCQZVSBWNOVQCQZWPWNBAWEVMWFAB
      UTZUCUBBVPCQZWQCFIUDWQBWSMBVPCUEUFUGWNVQCUHUIBWOVRCBWOVKVONZVQNVRBWNWTVQB
      AVKVMVOBAVJWGULBVMVNABWKWLVAULUMVBVKVOVQVCVDURUSVMVLVONZCQZVSVMVOCQZXBVMV
      NCQZXCCEHUDXCVMXDMVMVNCUEUFUGVLVOCUHVEVMXAVRCVMABVONZNWJXAVRVMAVKXEWIVMAV
      JWDULVMXEVQVONWIVMBVQVOVMBVPWRULVBVQVOUOVFUMABVOVGWMUQURUSVH $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x A $.
    $( There is at most one set equal to a class. $)
    moeq $p |- E* x x = A $=
      ( cv wceq wmo wex weu wi cvv wcel isset eueq bitr3i biimpi df-mo mpbir )
      ACBDZAEQAFZQAGZHRSRBIJSABKABLMNQAOP $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y ph $.  $d x y ps $.  $d x y A $.  $d x y B $.  $d x y C $.
    moeq3.1 $e |- B e. _V $.
    moeq3.2 $e |- C e. _V $.
    moeq3.3 $e |- -. ( ph /\ ps ) $.
    $( "At most one" property of equality (split into 3 cases).  (The first 2
       hypotheses could be eliminated with longer proof.) $)
    moeq3 $p |- E* x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( vy cvv wcel cv wceq wa wo wn w3o wmo weu eqeq2 anbi2d biidd 3orbi123d
      eubidv visset eueq3 vtoclg eumo syl wi wal eleq1 mpbii pm2.21 syl5
      anim2d orim1d 3orass 3imtr4g 19.21aiv euimmo mpi pm2.61i ) DKLZACMZDNZOZA
      BPQVFENOZBVFFNOZRZCSZVEVKCTZVLAVFJMZNZOZVIVJRZCTZVMJDKVNDNZVQVKCVSVPVHVIV
      IVJVJVSVOVGAVNDVFUAUBVSVIUCVSVJUCUDUEABCVNEFJUFGHIUGZUHVKCUIUJVEQZVRVLVTW
      AVKVQUKZCULVRVLUKWAWBCWAVHVIVJPZPVPWCPVKVQWAVHVPWCWAVGVOAVGVEWAVOVGVFKLVE
      CUFVFDKUMUNVEVOUOUPUQURVHVIVJUSVPVIVJUSUTVAVKVQCVBUJVCVD $.
      $( [23-Apr-1995] $)
  $}

  ${
    $d x y A $.
    mosub.1 $e |- E* x ph $.
    $( "At most one" remains true after substitution. $)
    mosub $p |- E* x E. y ( y = A /\ ph ) $=
      ( cv wceq wmo wal wa wex moeq ax-gen moexexv mp2an ) CFDGZCHABHZCIPAJCKBH
      CDLQCEMPACBNO $.
      $( [9-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Theorem for inferring "at most one." $)
    mo2icl $p |- ( A. x ( ph -> x = A ) -> E* x ph ) $=
      ( vy cvv wcel cv wceq wi wal wmo eqeq2 imbi2d albidv imbi1d wex 19.8a
      ax-17 mo2 sylibr vtoclg wn visset eleq1 mpbii imim2i con3rr3 alimdv
      alnex exmo ori sylbi syl6 pm2.61i ) CEFZABGZCHZIZBJZABKZIZAUPDGZHZIZBJZUT
      IVADCEVBCHZVEUSUTVFVDURBVFVCUQAVBCUPLMNOVEVEDPUTVEDQABDADRSTUAUOUBZUSAUBZ
      BJZUTVGURVHBURAUOUQUOAUQUPEFUOBUCUPCEUDUEUFUGUHVIABPZUBUTABUIVJUTABUJUKUL
      UMUN $.
      $( [17-Oct-1996] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x y ps $.
    moi2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Consequence of "at most one." $)
    moi2 $p |- ( ( ( A e. B /\ E* x ph ) /\ ( ph /\ ps ) ) -> x = A ) $=
      ( vy wcel wmo wa cv wceq wsbc wi wal hbs1 sbequ12 mo4f wex wb visset
      eqvinc ax-17 hbbi bicomd sylan9bb 19.23ai sylbi anbi2d eqeq2 imbi12d
      cla4gv a4sd syl5bi imp31 ) DEHZACIZABJZCKZDLZUQAACGKZMZJZUSVALZNZGOZCOUPU
      RUTNZAVBCGACGPZACGQZRUPVFVGCVEVGGDEVADLZVCURVDUTVJVBBAVJVDUTJZCSVBBTZCVAD
      GUAUBVKVLCVBBCVHBCUCUDVDVBAUTBVDAVBVIUEFUFUGUHUIVADUSUJUKULUMUNUO $.
      $( [29-Jun-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.  $d y ph $.  $d x y ps $.
    moi.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    moi.2 $e |- ( x = B -> ( ph <-> ch ) ) $.
    $( MAY BE REVISED - is there a way to shorten w/ moi2? $)
    $( Equality implied by "at most one." $)
    moi $p |- ( ( ( A e. C /\ B e. D ) /\ E* x ph /\ ( ps /\ ch ) ) ->
              A = B ) $=
      ( vy wcel wa wmo wceq cv wsbc wi wal hbs1 sbequ12 mo4f ax-17 hban hbim
      hbal anbi1d eqeq1 imbi12d albidv cla4gf wex wb visset eqvinc hbbi bicomd
      sylan9bb 19.23ai sylbi anbi2d eqeq2 cla4gv sylan9 syl5bi 3imp ) EGLZFHLZM
      ZADNZBCMZEFOZVJAADKPZQZMZDPZVMOZRZKSZDSZVIVKVLRZAVNDKADKTZADKUAZUBVGVTBVN
      MZEVMOZRZKSZVHWAVSWGDKEGVMELDUCWFDKWDWEDBVNDBDUCWBUDWEDUCUEUFVPEOZVRWFKWH
      VOWDVQWEWHABVNIUGVPEVMUHUIUJUKWFWAKFHVMFOZWDVKWEVLWIVNCBWIVQVPFOZMZDULVNC
      UMZDVMFKUNUOWKWLDVNCDWBCDUCUPVQVNAWJCVQAVNWCUQJURUSUTVAVMFEVBUIVCVDVEVF
      $.
      $( [18-Feb-2006] $)
  $}

  ${
    $d B x y $.  $d A x y $.  $d ph y $.  $d ps x y $.
    morex.1 $e |- B e. _V $.
    morex.2 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Derive membership from uniqueness.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    morex $p |- ( ( E. x e. A ph /\ E* x ph ) -> ( ps -> B e. A ) ) $=
      ( wmo wrex wcel wi cv wa wex df-rex exancom bitri wal hbmo1 hbe1 hban
      mopick 19.21ai wceq eleq1 imbi12d cla4v syl sylan2b ancoms ) ACHZACDIZBED
      JZKZULUKACLZDJZMZCNZUNULUPAMCNURACDOUPACPQUKURMZAUPKZCRUNUSUTCUKURCACSUQC
      TUAAUPCUBUCUTUNCEFUOEUDABUPUMGUOEDUEUFUGUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x ph $.  $d x A $.
    euxfr2.1 $e |- A e. _V $.
    euxfr2.2 $e |- E* y x = A $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    euxfr2 $p |- ( E! x E. y ( x = A /\ ph ) <-> E! y ph ) $=
      ( cv wceq wa wex weu wmo wi 2euswap moani ancom mobii mpbi mpg moeq
      impbii biidd ceqsexv eubii bitri ) BGDHZAIZCJBKZUGBJZCKZACKUHUJUGCLZUHUJM
      BUGBCNAUFIZCLUKUFACFOULUGCAUFPZQRSUGBLZUJUHMCUGCBNULBLUNUFABBDTOULUGBUMQR
      SUAUIACAABDEUFAUBUCUDUE $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.
    euxfr.1 $e |- A e. _V $.
    euxfr.2 $e |- E! y x = A $.
    euxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    euxfr $p |- ( E! x ph <-> E! y ps ) $=
      ( weu cv wceq wa wex euex ax-mp biantrur 19.41v pm5.32i exbii 3bitr2i
      eubii eumoi euxfr2 bitri ) ACICJEKZBLZDMZCIBDIAUGCAUEDMZALUEALZDMUGUHAUED
      IUHGUEDNOPUEADQUIUFDUEABHRSTUABCDEFUEDGUBUCUD $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d y z w ph $.  $d x z ps $.  $d y z w A $.  $d x z B $.  $d x y w $.
    euind.1 $e |- B e. _V $.
    euind.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    euind.3 $e |- ( x = y -> A = B ) $.
    $( Existential uniqueness via an indirect equality. $)
    euind $p |- ( ( A. x A. y ( ( ph /\ ps ) -> A = B ) /\ E. x ph )
                 -> E! z A. x ( ph -> z = A ) ) $=
      ( vw wa wceq wi wal wex cv weu cbvexv isseti biantrur exbii 19.41v excom
      bitr3i 3bitri wb eqeq2 imim2i bi2 an32 ancom anbi1i imbi1i impexp
      3bitr3i sylib syl 2alimi 19.23v albii 19.21v bitri eximdv syl5bi imp
      prth pm4.24 biimpi eqtr3 imim12i alanimi com12 syl5 19.21aivv adantl
      eqeq1 imbi2d albidv eu4 sylanbrc ) ABLZFGMZNZDOCOZACPZLAEQZFMZNZCOZEPZWJA
      KQZFMZNZCOZLZWGWLMZNZKOEOZWJERWEWFWKWFWGGMZBLZDPZEPZWEWKWFBDPWTEPZBLZDPZX
      CABCDISBXEDXDBEGHTUAUBXFXAEPZDPXCXGXEDWTBEUCUBXADEUDUEUFWEXBWJEWEXAWINZDO
      ZCOZXBWJNZWDXHCDWDWBWHWTUGZNZXHWCXLWBFGWGUHUIXMWBWTWHNZNZXHXLXNWBWHWTUJUI
      WBWTLZWHNXAALZWHNXOXHXPXQWHXPAWTLZBLWTALZBLXQABWTUKXRXSBAWTULUMWTABUKUFUN
      WBWTWHUOXAAWHUOUPUQURUSXJXBWINZCOXKXIXTCXAWIDUTVAXBWICVBVCUQVDVEVFWFWSWEW
      FWREKWPAWQNZCOZWFWQWIWNYACWIWNLAALZWHWMLZNYAAWHAWMVGAYCYDWQAYCAVHVIWGWLFV
      JVKURVLYBWFWQYBWFWQNAWQCUTVIVMVNVOVPWJWOEKWQWIWNCWQWHWMAWGWLFVQVRVSVTWA
      $.
      $( [11-Oct-2010] $)
  $}

  $( Restricted unique existence implies restricted existence. $)
  reurex $p |- ( E! x e. A ph -> E. x e. A ph ) $=
    ( cv wcel wa weu wex wreu wrex euex df-reu df-rex 3imtr4i ) BDCEAFZBGOBHABC
    IABCJOBKABCLABCMN $.
    $( [19-Aug-1999] $)

  $( Restricted uniqueness in terms of "at most one." $)
  reu5 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\ E* x ( x e. A /\ ph ) ) ) $=
    ( cv wcel wa weu wex wmo wreu wrex eu5 df-reu df-rex anbi1i 3bitr4i ) BDCEA
    FZBGQBHZQBIZFABCJABCKZSFQBLABCMTRSABCNOP $.
    $( [23-May-1999] $)

  ${
    $d x y A $.  $d y ph $.
    $( A way to express restricted uniqueness. $)
    reu2 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
               A. x e. A A. y e. A ( ( ph /\ [ y / x ] ph ) -> x = y ) ) ) $=
      ( cv wcel wa weu wex wsbc wceq wi wal wreu wrex wral ax-17 eu2 df-reu
      df-rex df-ral 19.21v hbs1 hban eleq1 sbequ12 anbi12d sbie anbi2i an4
      bitri imbi1i impexp 3bitri albii imbi2i 3bitr4i bitr4i anbi12i ) BEZDFZAG
      ZBHVBBIZVBVBBCEZJZGZUTVDKZLZCMZBMZGABDNABDOZAABVDJZGZVGLZCDPZBDPZGVBBCVBC
      QRABDSVKVCVPVJABDTVPVAVOLZBMVJVOBDUAVIVQBVAVDDFZVNLZLZCMVAVSCMZLVIVQVAVSC
      UBVHVTCVHVAVRGZVMGZVGLWBVNLVTVFWCVGVFVBVRVLGZGWCVEWDVBVBWDBCVRVLBVRBQABCU
      CUDVGVAVRAVLUTVDDUEABCUFUGUHUIVAAVRVLUJUKULWBVMVGUMVAVRVNUMUNUOVOWAVAVNCD
      UAUPUQUOURUSUQ $.
      $( [22-Nov-1994] $)

    $( A way to express restricted uniqueness. $)
    reu6 $p |- ( E! x e. A ph <-> E. y e. A A. x e. A ( ph <-> x = y ) ) $=
      ( wreu cv wcel wa weu wceq wb wal wex wral wrex df-reu df-eu wi 19.28v
      wsbc eleq1 sbequ12 anbi12d eqeq1 bibi12d eqid tbt simpl sylbir syl6bi
      a4imv bi1 expdimp bi2 simpr syl6 adantr impbid ex a4s jca a5i imim2i
      imp3a adantl eleq1a imp com23 adantll jcai alimi impbii df-ral anbi2i
      3bitr4i exbii df-rex bitr4i 3bitri ) ABDEBFZDGZAHZBIWBVTCFZJZKZBLZCMZAWDK
      ZBDNZCDOZABDPWBBCQWGWCDGZWIHZCMWJWFWLCWKWAWHRZHZBLZWKWMBLZHWFWLWKWMBSWFWO
      WEWNBWFWKWMWEWKBCWDWEWKABWCTZHZWCWCJZKZWKWDWBWRWDWSWDWAWKAWQVTWCDUAABCUBU
      CVTWCWCUDUEWTWRWKWSWRWCUFUGWKWQUHUIUJUKWEWMBWEWAWHWEWAHAWDWEWAAWDWBWDULUM
      WEWDARZWAWEWDWBAWBWDUNWAAUOUPUQURUSUTVAVBWNWEBWNWBWDWMWBWDRWKWMWAAWDWHAWD
      RWAAWDULVCVDVEWNWDWBWNWDHWAAWNWDWAWKWDWARWMWCDVTVFUQVGWMWDWAARZWKWMWDXBWM
      WAWDAWHXAWAAWDUNVCVHVGVIVJUSURVKVLWIWPWKWHBDVMVNVOVPWICDVQVRVS $.
      $( [20-Oct-2006] $)

    $( A way to express restricted uniqueness. $)
    reu3 $p |- ( E! x e. A ph <->
               ( E. x e. A ph /\ E. y e. A A. x e. A ( ph -> x = y ) ) ) $=
      ( wreu wrex cv wceq wi wral wa reurex wb reu6 bi1 ralimi reximi sylbi
      jca wex rexex anim2i wcel weu wal ax-17 eu3 df-reu df-rex df-ral impexp
      albii bitr4i exbii anbi12i 3bitr4i sylibr impbii ) ABDEZABDFZABGZCGHZIZBD
      JZCDFZKZUSUTVEABDLUSAVBMZBDJZCDFVEABCDNVHVDCDVGVCBDAVBOPQRSVFUTVDCTZKZUSV
      EVIUTVDCDUAUBVADUCZAKZBUDVLBTZVLVBIZBUEZCTZKUSVJVLBCVLCUFUGABDUHUTVMVIVPA
      BDUIVDVOCVDVKVCIZBUEVOVCBDUJVNVQBVKAVBUKULUMUNUOUPUQUR $.
      $( [24-Oct-2006] $)
  $}

  ${
    $d x y z A $.  $d y z ph $.  $d x z ps $.
    rmo4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Restricted "at most one" using implicit substitution. $)
    rmo4 $p |- ( E* x ( x e. A /\ ph ) <->
               A. x e. A A. y e. A ( ( ph /\ ps ) -> x = y ) ) $=
      ( cv wcel wa wceq wi wal wral wmo an4 ancom anbi1i bitri imbi1i impexp
      3bitri albii df-ral r19.21v 3bitr2i eleq1 anbi12d mo4 3bitr4i ) CGZEHZAIZ
      DGZEHZBIZIZUJUMJZKZDLZCLUKABIZUQKZDEMZKZCLULCNVBCEMUSVCCUSUNUKVAKZKZDLVDD
      EMVCURVEDURUNUKIZUTIZUQKVFVAKVEUPVGUQUPUKUNIZUTIVGUKAUNBOVHVFUTUKUNPQRSVF
      UTUQTUNUKVATUAUBVDDEUCUKVADEUDUEUBULUOCDUQUKUNABUJUMEUFFUGUHVBCEUCUI $.
      $( [24-Oct-2006] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu4 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
             A. x e. A A. y e. A ( ( ph /\ ps ) -> x = y ) ) ) $=
      ( wreu wrex cv wcel wa wmo wceq wi wral reu5 rmo4 anbi2i bitri ) ACEGACEH
      ZCIZEJAKCLZKTABKUADIMNDEOCEOZKACEPUBUCTABCDEFQRS $.
      $( [23-Nov-1994] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu7 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
             E. x e. A A. y e. A ( ps -> x = y ) ) ) $=
      ( vz wreu wrex cv wceq wi wral wa reu3 eqeq1 eqcom syl6bb imbi12d
      cbvralv rexbii imbi2d ralbidv cbvrexv bitri anbi2i ) ACEHACEIZACJZGJZKZLZ
      CEMZGEIZNUGBUHDJZKZLZDEMZCEIZNACGEOUMURUGUMBUIUNKZLZDEMZGEIURULVAGEUKUTCD
      EUOABUJUSFUOUJUNUIKUSUHUNUIPUNUIQRSTUAVAUQGCEUIUHKZUTUPDEVBUSUOBUIUHUNPUB
      UCUDUEUFUE $.
      $( [24-Oct-2006] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu8 $p |- ( E! x e. A ph <-> E. x e. A ( ph /\
                A. y e. A ( ps -> x = y ) ) ) $=
      ( wreu cv wceq wb wral wrex wi wa cbvreuv reu6 wcel dfbi2 ralbii ancom
      eqcom imbi2i a1i biimt wal df-ral bi2.04 albii visset eleq1 imbi12d
      bicomd eqcoms ceqsalv 3bitrri syl6bb anbi12d syl5bb r19.26 syl6rbbr
      rexbiia 3bitri ) ACEGBDEGBDHZCHZIZJZDEKZCELABVDVCIZMZDEKZNZCELABCDEFOBDCE
      PVGVKCEVGBVEMZVEBMZNZDEKZVDEQZVKVFVNDEBVERSVPVKVLDEKZVMDEKZNZVOVKVJANVPVS
      AVJTVPVJVQAVRVJVQJVPVIVLDEVHVEBVDVCUAUBSUCVPAVPAMZVRVPAUDVRVCEQZVMMZDUEVE
      WABMZMZDUEVTVMDEUFWBWDDWAVEBUGUHWCVTDVDCUIWCVTJVDVCVHVTWCVHVPWAABVDVCEUJF
      UKULUMUNUOUPUQURVLVMDEUSUTURVAVB $.
      $( [24-Oct-2006] $)
  $}

  ${
    $d x y A $.
    $( A condition allowing swap of uniqueness and existential quantifiers. $)
    2reuswap $p |- ( A. x e. A E* y ( y e. A /\ ph ) ->
                   ( E! x e. A E. y e. A ph -> E! y e. A E. x e. A ph ) ) $=
      ( cv wcel wa wmo wral wal wrex wreu wi df-ral moanimv albii bitr4i wex
      weu 2euswap df-reu df-rex r19.42v an12 exbii 3bitr3i eubii bitri bitr3i
      3imtr4g sylbi ) CEDFZAGZCHZBDIZBEDFZUMGZCHZBJZACDKZBDLZABDKZCDLZMUOUPUNMZ
      BJUSUNBDNURVDBUPUMCOPQUSUQCRZBSZUQBRZCSZVAVCUQBCTVAUPUTGZBSVFUTBDUAVIVEBU
      PAGZCDKULVJGZCRVIVEVJCDUBUPACDUCVKUQCULUPAUDUEUFUGUHVCULVBGZCSVHVBCDUAVLV
      GCVLUMBDKVGULABDUCUMBDUBUIUGUHUJUK $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d w y z A $.  $d x z B $.  $d w x y z C $.  $d w y z ph $.  $d x z ps $.
    reuind.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    reuind.2 $e |- ( x = y -> A = B ) $.
    $( Existential uniqueness via an indirect equality. $)
    reuind $p |- ( ( A. x A. y ( ( ( A e. C /\ ph ) /\ ( B e. C /\ ps ) )
         -> A = B ) /\ E. x ( A e. C /\ ph ) )
                 -> E! z e. C A. x ( ( A e. C /\ ph ) -> z = A ) ) $=
      ( vw wcel wa wceq wi wal wex cv wrex wral wreu eleq1d anbi12d cbvexv
      risset anbi1i exbii rexcom4 r19.41v bitr2i 3bitri wb eqeq2 imim2i bi2
      an32 ancom imbi1i impexp 3bitr3i sylib syl 2alimi 19.23v an12 eleq1
      adantr pm5.32ri bitr4i 19.42v bitri albii 19.21v exp3a reximdvai syl5bi
      imp prth pm4.24 biimpi eqtr3 imim12i alanimi com12 syl5 a1d r19.21aivv
      adantl eqeq1 imbi2d albidv reu4 sylanbrc ) FHLZAMZGHLZBMZMZFGNZOZDPCPZWOC
      QZMWOERZFNZOZCPZEHSZXFWOKRZFNZOZCPZMZXCXHNZOZKHTEHTZXFEHUAXAXBXGXBXCGNZBM
      ZDQZEHSZXAXGXBWQDQXPEHSZBMZDQZXSWOWQCDCRDRNZWNWPABYCFGHJUBIUCUDWQYADWPXTB
      EGHUEUFUGXSXQEHSZDQYBXQEDHUHYDYADXPBEHUIUGUJUKXAXRXFEHXAXCHLZXRXFXAXPWQMZ
      XEOZDPZCPZYEXRMZXFOZWTYGCDWTWRXDXPULZOZYGWSYLWRFGXCUMUNYMWRXPXDOZOZYGYLYN
      WRXDXPUOUNWRXPMZXDOYFWOMZXDOYOYGYPYQXDYPWOXPMZWQMXPWOMZWQMYQWOWQXPUPYRYSW
      QWOXPUQUFXPWOWQUPUKURWRXPXDUSYFWOXDUSUTVAVBVCYIYJXEOZCPYKYHYTCYHYFDQZXEOY
      TYFXEDVDUUAYJXEUUAYEXQMZDQYJYFUUBDYFWPXQMUUBXPWPBVEXQYEWPXPYEWPULBXCGHVFV
      GVHVIUGYEXQDVJVKURVKVLYJXECVMVKVAVNVOVPVQXBXOXAXBXNEKHHXBXNYEXHHLMXLWOXMO
      ZCPZXBXMXEXJUUCCXEXJMWOWOMZXDXIMZOUUCWOXDWOXIVRWOUUEUUFXMWOUUEWOVSVTXCXHF
      WAWBVBWCUUDXBXMUUDXBXMOWOXMCVDVTWDWEWFWGWHXFXKEKHXMXEXJCXMXDXIWOXCXHFWIWJ
      WKWLWM $.
      $( [16-Oct-2010] $)
  $}

