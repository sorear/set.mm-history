$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The universal class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the universal class. $)
  $c _V $. $( Letter V (for the universal class) $)

  $( Extend class notation to include the universal class symbol. $)
  cvv $a class _V $.

  ${
    $d z x $.  $d z y $.
    $( Soundness justification theorem for ~ df-v .  (Contributed by Rodolfo
       Medina, 27-Apr-2010.) $)
    vjust $p |- { x | x = x } = { y | y = y } $=
      ( vz cv wceq cab wsbc wcel equid sbt 2th df-clab 3bitr4i eqriv ) CADZOEZA
      FZBDZREZBFZPACDZGZSBUAGZUAQHUATHUBUCPACAIJSBCBIJKPCALSCBLMN $.
      $( [19-May-2010] $) $( [27-Apr-2010] $)
  $}

  $( Define the universal class.  Definition 5.20 of [TakeutiZaring] p. 21. $)
  df-v $a |- _V = { x | x = x } $.

  $( All set variables are sets (see ~ isset ).  Theorem 6.8 of [Quine]
     p. 43. $)
  visset $p |- x e. _V $=
    ( cv cvv wcel wceq eqid df-v abeq2i mpbir ) ABZCDJJEZJFKACAGHI $.
    $( [5-Aug-1993] $)

  ${
    $d x A $.
    $( Two ways to say " ` A ` is a set":  A class ` A ` is a member of the
       universal class ` _V ` (see ~ df-v ) if and only if the class ` A `
       exists (i.e. there exists some set ` x ` equal to class ` A ` ).
       Theorem 6.9 of [Quine] p. 43. _Notational convention_:  We will use the
       notational device " ` A e. _V ` " to mean " ` A ` is a set" very
       frequently, for example in ~ uniex .  Note the when ` A ` is not a set,
       it is called a proper class.  In some theorems, such as ~ uniexg , in
       order to shorten certain proofs we use the more general antecedent
       ` A e. B ` instead of ` A e. _V ` to mean " ` A ` is a set."

       Note that a constant is considered distinct from all variables.  This is
       why ` _V ` is not included in the distinct variable list, even though
       ~ df-clel requires that the expression substituted for ` B ` not contain
       ` x ` .  (Also, the Metamath spec does not allow constants in the
       distinct variable list.) $)
    isset $p |- ( A e. _V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wa wex df-clel visset biantru exbii bitr4i ) BCDAEZBFZ
      NCDZGZAHOAHABCIOQAPOAJKLM $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    isseti.1 $e |- A e. _V $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    isseti $p |- E. x x = A $=
      ( cvv wcel cv wceq wex isset mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.
    issetri.1 $e |- E. x x = A $.
    $( A way to say " ` A ` is a set" (inference rule). $)
    issetri $p |- A e. _V $=
      ( cvv wcel cv wceq wex isset mpbir ) BDEAFBGAHCABIJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( If a class is a member of another class, it is a set.  Theorem 6.12 of
       [Quine] p. 44.  (The proof was shortened by Andrew Salmon,
       8-Jun-2011.) $)
    elisset $p |- ( A e. B -> A e. _V ) $=
      ( vx cv wceq wcel wa wex cvv exsimpl df-clel isset 3imtr4i ) CDZAEZNBFZGC
      HOCHABFAIFOPCJCABKCALM $.
      $( [8-Jun-2011] $) $( [5-Aug-1993] $)

    $( If a class is a member of another class, it is a set.  Theorem 6.12 of
       [Quine] p. 44. $)
    elissetOLD $p |- ( A e. B -> A e. _V ) $=
      ( vx wcel cv wceq wex cvv wa df-clel 19.40 sylbi pm3.26d isset sylibr )
      ABDZCEZAFZCGZAHDPSQBDZCGZPRTICGSUAICABJRTCKLMCANO $.
      $( [5-Aug-1993] $)
  $}

  ${
    elisseti.1 $e |- A e. B $.
    $( If a class is a member of another class, it is a set. $)
    elisseti $p |- A e. _V $=
      ( wcel cvv elisset ax-mp ) ABDAEDCABFG $.
      $( [11-Jun-1994] $)
  $}

  ${
    $d x A $.
    $( An element of a class exists. $)
    elex $p |- ( A e. B -> E. x x = A ) $=
      ( wcel cvv cv wceq wex elisset isset sylib ) BCDBEDAFBGAHBCIABJK $.
      $( [1-May-1995] $)
  $}

  $( A universal quantifier restricted to the universe is unrestricted. $)
  ralv $p |- ( A. x e. _V ph <-> A. x ph ) $=
    ( cvv wral cv wcel wi wal df-ral visset a1bi albii bitr4i ) ABCDBECFZAGZBHA
    BHABCIAOBNABJKLM $.
    $( [31-Mar-2004] $) $( [26-Mar-2004] $)

  $( An existential quantifier restricted to the universe is unrestricted. $)
  rexv $p |- ( E. x e. _V ph <-> E. x ph ) $=
    ( cvv wrex cv wcel wa wex df-rex visset biantrur exbii bitr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [4-Apr-2004] $) $( [26-Mar-2004] $)

  $( A uniqueness quantifier restricted to the universe is unrestricted. $)
  reuv $p |- ( E! x e. _V ph <-> E! x ph ) $=
    ( cvv wreu cv wcel wa weu df-reu visset biantrur eubii bitr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [7-Jun-2011] $) $( [1-Nov-2010] $)

  $( A class abstraction restricted to the universe is unrestricted.  (The
     proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
  rabab $p |- { x e. _V | ph } = { x | ph } $=
    ( cvv crab cv wcel wa cab df-rab visset biantrur abbii eqtr4i ) ABCDBECFZAG
    ZBHABHABCIAOBNABJKLM $.
    $( [8-Jun-2011] $) $( [27-Dec-2004] $)

  $( A class abstraction restricted to the universe is unrestricted. $)
  rababOLD $p |- { x e. _V | ph } = { x | ph } $=
    ( cvv crab cv wcel wa cab df-rab pm3.27 visset jctl impbii abbii eqtri ) AB
    CDBECFZAGZBHABHABCIQABQAPAJAPBKLMNO $.
    $( [1-Jan-2005] $) $( [27-Dec-2004] $)

  ${
    $d x y $.  $d y A $.
    $( Commutation of restricted and unrestricted universal quantifiers.  (The
       proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
    ralcom4 $p |- ( A. x e. A A. y ph <-> A. y A. x e. A ph ) $=
      ( cvv wral wal ralcom ralv ralbii 3bitr3i ) ACEFZBDFABDFZCEFACGZBDFMCGABC
      DEHLNBDACIJMCIK $.
      $( [8-Jun-2011] $) $( [26-Mar-2004] $)

    $d x y $.  $d y A $.
    $( Commutation of restricted and unrestricted universal quantifiers. $)
    ralcom4OLD $p |- ( A. x e. A A. y ph <-> A. y A. x e. A ph ) $=
      ( wral cvv wal ralcom ralv ralbii 3bitr3ri ) ABDEZCFEACFEZBDELCGACGZBDEAC
      BFDHLCIMNBDACIJK $.
      $( [31-Mar-2004] $) $( [26-Mar-2004] $)

    $( Commutation of restricted and unrestricted existential quantifiers.
       (The proof was shortened by Andrew Salmon, 8-Jun-2011.) $)
    rexcom4 $p |- ( E. x e. A E. y ph <-> E. y E. x e. A ph ) $=
      ( cvv wrex wex rexcom rexv rexbii 3bitr3i ) ACEFZBDFABDFZCEFACGZBDFMCGABC
      DEHLNBDACIJMCIK $.
      $( [8-Jun-2011] $) $( [12-Apr-2004] $)

    $( Commutation of restricted and unrestricted existential quantifiers. $)
    rexcom4OLD $p |- ( E. x e. A E. y ph <-> E. y E. x e. A ph ) $=
      ( wrex cvv wex rexcom rexv rexbii 3bitr3ri ) ABDEZCFEACFEZBDELCGACGZBDEAC
      BFDHLCIMNBDACIJK $.
      $( [13-Apr-2004] $) $( [12-Apr-2004] $)
  $}

  ${
    $d x A $.
    ceqsalg.1 $e |- ( ps -> A. x ps ) $.
    ceqsalg.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    ceqsalg $p |- ( A e. B -> ( A. x ( x = A -> ph ) <-> ps ) ) $=
      ( wcel cv wceq wi wal wex hba1 biimpd a2i a4s 19.23ad elex syl5com
      biimprcd 19.21ai impbid1 ) DEHZCIDJZAKZCLZBUGUECMBUDUGUEBCUFCNFUFUEBKCUEA
      BUEABGOPQRCDESTBUFCFUEABGUAUBUC $.
      $( [8-Jun-2011] $) $( [29-Oct-2003] $)

    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsalgOLD $p |- ( A e. B -> ( A. x ( x = A -> ph ) <-> ps ) ) $=
      ( wcel cv wceq wi wal wex biimpd a2i alimi 19.23 sylib elex syl5com
      biimprcd 19.21ai impbid1 ) DEHZCIDJZAKZCLZBUGUECMZBUDUGUEBKZCLUHBKUFUICUE
      ABUEABGNOPUEBCFQRCDESTBUFCFUEABGUAUBUC $.
      $( [29-Oct-2003] $)
  $}

  ${
    $d x A $.
    ceqsal.1 $e |- ( ps -> A. x ps ) $.
    ceqsal.2 $e |- A e. _V $.
    ceqsal.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsal $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( cvv wcel cv wceq wi wal wb ceqsalg ax-mp ) DHICJDKALCMBNFABCDHEGOP $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsalv.1 $e |- A e. _V $.
    ceqsalv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsalv $p |- ( A. x ( x = A -> ph ) <-> ps ) $=
      ( ax-17 ceqsal ) ABCDBCGEFH $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x ps $.
    gencl.1 $e |- ( th <-> E. x ( ch /\ A = B ) ) $.
    gencl.2 $e |- ( A = B -> ( ph <-> ps ) ) $.
    gencl.3 $e |- ( ch -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    gencl $p |- ( th -> ps ) $=
      ( wceq wa wex syl5bi impcom 19.23aiv sylbi ) DCFGKZLZEMBHSBERCBRABCIJNOPQ
      $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y $.  $d x R $.  $d x ps $.  $d y C $.  $d y S $.  $d y ch $.
    2gencl.1 $e |- ( C e. S <-> E. x ( x e. R /\ A = C ) ) $.
    2gencl.2 $e |- ( D e. S <-> E. y ( y e. R /\ B = D ) ) $.
    2gencl.3 $e |- ( A = C -> ( ph <-> ps ) ) $.
    2gencl.4 $e |- ( B = D -> ( ps <-> ch ) ) $.
    2gencl.5 $e |- ( ( x e. R /\ y e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    2gencl $p |- ( ( C e. S /\ D e. S ) -> ch ) $=
      ( wcel wi cv wceq imbi2d ex gencl com12 impcom ) IKQZHKQZCUGBRUGCRESJQZUF
      EGIMGITBCUGOUAUGUHBUHARUHBRDSJQZUGDFHLFHTABUHNUAUIUHAPUBUCUDUCUE $.
      $( [17-May-1996] $)
  $}

  ${
    $d x y z $.  $d y z D $.  $d z F $.  $d x y R $.  $d y z S $.  $d x ps $.
    $d y ch $.  $d z th $.
    3gencl.1 $e |- ( D e. S <-> E. x ( x e. R /\ A = D ) ) $.
    3gencl.2 $e |- ( F e. S <-> E. y ( y e. R /\ B = F ) ) $.
    3gencl.3 $e |- ( G e. S <-> E. z ( z e. R /\ C = G ) ) $.
    3gencl.4 $e |- ( A = D -> ( ph <-> ps ) ) $.
    3gencl.5 $e |- ( B = F -> ( ps <-> ch ) ) $.
    3gencl.6 $e |- ( C = G -> ( ch <-> th ) ) $.
    3gencl.7 $e |- ( ( x e. R /\ y e. R /\ z e. R ) -> ph ) $.
    $( Implicit substitution for class with embedded variable. $)
    3gencl $p |- ( ( D e. S /\ F e. S /\ G e. S ) -> th ) $=
      ( wcel wa wi cv wceq imbi2d 3expia 2gencl com12 gencl 3impia ) KMUCZNMUCZ
      OMUCZDUPUNUOUDZDUQCUEUQDUEGUFLUCZUPGJORJOUGCDUQUAUHUQURCURAUEURBUEURCUEEF
      HIKNLMPQHKUGABURSUHINUGBCURTUHEUFLUCFUFLUCURAUBUIUJUKULUKUM $.
      $( [17-May-1996] $)
  $}

  ${
    $d x A $.  $d x ps $.
    cgsexg.1 $e |- ( x = A -> ch ) $.
    cgsexg.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsexg $p |- ( A e. B ->
                     ( E. x ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aiv biimprcd ancld eximdv cv wceq elex eximi
      syl syl5com impbid2 ) EFIZCAJZDKZBUEBDCABHLMBCDKZUFUDBCUEDBCACABHNOPUDDQE
      RZDKUGDEFSUHCDGTUAUBUC $.
      $( [21-Apr-2008] $) $( [26-Aug-2007] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    cgsex2g.1 $e |- ( ( x = A /\ y = B ) -> ch ) $.
    cgsex2g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for general classes. $)
    cgsex2g $p |- ( ( A e. C /\ B e. D ) ->
                     ( E. x E. y ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aivv biimprcd ancld 2eximdv cv wceq elex
      anim12i eeanv sylibr 2eximi syl syl5com impbid2 ) FHLZGILZMZCAMZENDNZBUMB
      DECABKOPBCENDNZUNULBCUMDEBCACABKQRSULDTFUAZETGUAZMZENDNZUOULUPDNZUQENZMUS
      UJUTUKVADFHUBEGIUBUCUPUQDEUDUEURCDEJUFUGUHUI $.
      $( [26-Jul-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $.
    cgsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) -> ch ) $.
    cgsex4g.2 $e |- ( ch -> ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 4 general classes. $)
    cgsex4g $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                    ( E. x E. y E. z E. w ( ch /\ ph ) <-> ps ) ) $=
      ( wcel wa wex biimpa 19.23aivv biimprcd ancld 2eximdv cv wceq elex
      anim12i eeanv sylibr ee4anv 2eximi syl syl5com impbid2 ) HLPZIMPZQZJLPZKM
      PZQZQZCAQZGRFRZERDRZBVCBDEVBBFGCABOSTTBCGRFRZERDRZVDVABVEVCDEBCVBFGBCACAB
      OUAUBUCUCVADUDHUEZEUDIUEZQZFUDJUEZGUDKUEZQZQZGRFRZERDRZVFVAVIERDRZVLGRFRZ
      QVOUQVPUTVQUQVGDRZVHERZQVPUOVRUPVSDHLUFEIMUFUGVGVHDEUHUIUTVJFRZVKGRZQVQUR
      VTUSWAFJLUFGKMUFUGVJVKFGUHUIUGVIVLDEFGUJUIVNVEDEVMCFGNUKUKULUMUN $.
      $( [5-Aug-1995] $)
  $}

  ${
    $d x A $.
    ceqsex.1 $e |- ( ps -> A. x ps ) $.
    ceqsex.2 $e |- A e. _V $.
    ceqsex.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsex $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( cv wceq wa wex biimpa 19.23ai isseti wi wal biimprcd 19.21ai exintr
      syl mpi impbii ) CHDIZAJZCKZBUDBCEUCABGLMBUCCKZUECDFNBUCAOZCPUFUEOBUGCEUC
      ABGQRUCACSTUAUB $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsexv.1 $e |- A e. _V $.
    ceqsexv.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexv $p |- ( E. x ( x = A /\ ph ) <-> ps ) $=
      ( ax-17 ceqsex ) ABCDBCGEFH $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    ceqsex2.1 $e |- ( ps -> A. x ps ) $.
    ceqsex2.2 $e |- ( ch -> A. y ch ) $.
    ceqsex2.3 $e |- A e. _V $.
    ceqsex2.4 $e |- B e. _V $.
    ceqsex2.5 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex2.6 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2 $p |- ( E. x E. y ( x = A /\ y = B /\ ph ) <-> ch ) $=
      ( cv wceq w3a wex wa 3anass exbii 19.42v bitri ax-17 hban hbex anbi2d
      exbidv ceqsex 3bitri ) DNFOZENGOZAPZEQZDQUJUKARZEQZRZDQUKBRZEQZCUMUPDUMUJ
      UNRZEQUPULUSEUJUKASTUJUNEUAUBTUOURDFUQDEUKBDUKDUCHUDUEJUJUNUQEUJABUKLUFUG
      UHBCEGIKMUHUI $.
      $( [13-Aug-2011] $) $( [7-Jun-2006] $)

    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2OLD $p |- ( E. x E. y ( ( x = A /\ y = B ) /\ ph ) <-> ch ) $=
      ( cv wceq wa wex anass exbii 19.42v bitri ax-17 hban hbex anbi2d exbidv
      ceqsex 3bitri ) DNFOZENGOZPAPZEQZDQUIUJAPZEQZPZDQUJBPZEQZCULUODULUIUMPZEQ
      UOUKUREUIUJARSUIUMETUASUNUQDFUPDEUJBDUJDUBHUCUDJUIUMUPEUIABUJLUEUFUGBCEGI
      KMUGUH $.
      $( [10-Jun-2006] $) $( [7-Jun-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ps $.  $d y ch $.
    ceqsex2v.1 $e |- A e. _V $.
    ceqsex2v.2 $e |- B e. _V $.
    ceqsex2v.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex2v.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2v $p |- ( E. x E. y ( x = A /\ y = B /\ ph ) <-> ch ) $=
      ( ax-17 ceqsex2 ) ABCDEFGBDLCELHIJKM $.
      $( [13-Aug-2011] $) $( [7-Jun-2006] $)

    $( Elimination of two existential quantifiers, using implicit
       substitution.  (Contributed by Scott Fenton, 7-Jun-2006.) $)
    ceqsex2vOLD $p |- ( E. x E. y ( ( x = A /\ y = B ) /\ ph ) <-> ch ) $=
      ( ax-17 ceqsex2OLD ) ABCDEFGBDLCELHIJKM $.
      $( [10-Jun-2006] $) $( [7-Jun-2006] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x ps $.  $d y ch $.
    $d z th $.
    ceqsex3v.1 $e |- A e. _V $.
    ceqsex3v.2 $e |- B e. _V $.
    ceqsex3v.3 $e |- C e. _V $.
    ceqsex3v.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex3v.5 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex3v.6 $e |- ( z = C -> ( ch <-> th ) ) $.
    $( Elimination of three existential quantifiers, using implicit
       substitution. $)
    ceqsex3v $p |- ( E. x E. y E. z ( ( x = A /\ y = B /\ z = C ) /\ ph )
                 <-> th ) $=
      ( cv wceq w3a wa wex anass 3anass anbi1i df-3an anbi2i 3bitr4i 2exbii
      19.42vv bitri exbii 3anbi3d 2exbidv ceqsexv ceqsex2v 3bitri ) EQHRZFQIRZG
      QJRZSZATZGUAFUAZEUAUQURUSASZGUAFUAZTZEUAURUSBSZGUAFUAZDVBVEEVBUQVCTZGUAFU
      AVEVAVHFGUQURUSTZTZATUQVIATZTVAVHUQVIAUBUTVJAUQURUSUCUDVCVKUQURUSAUEUFUGU
      HUQVCFGUIUJUKVDVGEHKUQVCVFFGUQABURUSNULUMUNBCDFGIJLMOPUOUP $.
      $( [16-Aug-2011] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x ps $.  $d y ch $.  $d z th $.  $d w ta $.
    ceqsex4v.1 $e |- A e. _V $.
    ceqsex4v.2 $e |- B e. _V $.
    ceqsex4v.3 $e |- C e. _V $.
    ceqsex4v.4 $e |- D e. _V $.
    ceqsex4v.7 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex4v.8 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex4v.9 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex4v.10 $e |- ( w = D -> ( th <-> ta ) ) $.
    $( Elimination of four existential quantifiers, using implicit
       substitution. $)
    ceqsex4v $p |- ( E. x E. y E. z E. w
          ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) /\ ph ) <-> ta ) $=
      ( cv wceq wa w3a wex 19.42vv 3anass df-3an anbi2i bitr4i 2exbii 3bitr4i
      3anbi3d 2exbidv ceqsex2v 3bitri ) FUBJUCZGUBKUCZUDZHUBLUCZIUBMUCZUDZAUEZI
      UFHUFZGUFFUFURUSVAVBAUEZIUFHUFZUEZGUFFUFVAVBCUEZIUFHUFZEVEVHFGUTVFUDZIUFH
      UFUTVGUDVEVHUTVFHIUGVDVKHIVDUTVCAUDZUDVKUTVCAUHVFVLUTVAVBAUIUJUKULURUSVGU
      IUMULVGVAVBBUEZIUFHUFVJFGJKNOURVFVMHIURABVAVBRUNUOUSVMVIHIUSBCVAVBSUNUOUP
      CDEHILMPQTUAUPUQ $.
      $( [23-Sep-2011] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u E $.  $d x y z w v u F $.  $d x ps $.
    $d y ch $.  $d z th $.  $d w ta $.  $d v et $.  $d u ze $.
    ceqsex6v.1 $e |- A e. _V $.
    ceqsex6v.2 $e |- B e. _V $.
    ceqsex6v.3 $e |- C e. _V $.
    ceqsex6v.4 $e |- D e. _V $.
    ceqsex6v.5 $e |- E e. _V $.
    ceqsex6v.6 $e |- F e. _V $.
    ceqsex6v.7 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex6v.8 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex6v.9 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex6v.10 $e |- ( w = D -> ( th <-> ta ) ) $.
    ceqsex6v.11 $e |- ( v = E -> ( ta <-> et ) ) $.
    ceqsex6v.12 $e |- ( u = F -> ( et <-> ze ) ) $.
    $( Elimination of six existential quantifiers, using implicit
       substitution. $)
    ceqsex6v $p |- ( E. x E. y E. z E. w E. v E. u
          ( ( x = A /\ y = B /\ z = C ) /\ ( w = D /\ v = E /\ u = F ) /\ ph )
                 <-> ze ) $=
      ( cv wceq w3a wex wa 3anass 3exbii 19.42vvv bitri anbi2d 3exbidv
      ceqsex3v 3bitri ) HULNUMZIULOUMZJULPUMZUNZKULQUMLULRUMMULSUMUNZAUNZMUOLUO
      KUOZJUOIUOHUOVHVIAUPZMUOLUOKUOZUPZJUOIUOHUOVIDUPZMUOLUOKUOZGVKVNHIJVKVHVL
      UPZMUOLUOKUOVNVJVQKLMVHVIAUQURVHVLKLMUSUTURVMVIBUPZMUOLUOKUOVICUPZMUOLUOK
      UOVPHIJNOPTUAUBVEVLVRKLMVEABVIUFVAVBVFVRVSKLMVFBCVIUGVAVBVGVSVOKLMVGCDVIU
      HVAVBVCDEFGKLMQRSUCUDUEUIUJUKVCVD $.
      $( [21-Sep-2011] $)
  $}

  ${
    $d x y z w v u t s A $.  $d x y z w v u t s B $.  $d x y z w v u t s C $.
    $d x y z w v u t s D $.  $d x y z w v u t s E $.  $d x y z w v u t s F $.
    $d x y z w v u t s G $.  $d x y z w v u t s H $.  $d x ps $.  $d y ch $.
    $d z th $.  $d w ta $.  $d v et $.  $d u ze $.  $d t si $.  $d s rh $.
    ceqsex8v.1 $e |- A e. _V $.
    ceqsex8v.2 $e |- B e. _V $.
    ceqsex8v.3 $e |- C e. _V $.
    ceqsex8v.4 $e |- D e. _V $.
    ceqsex8v.5 $e |- E e. _V $.
    ceqsex8v.6 $e |- F e. _V $.
    ceqsex8v.7 $e |- G e. _V $.
    ceqsex8v.8 $e |- H e. _V $.
    ceqsex8v.9 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsex8v.10 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ceqsex8v.11 $e |- ( z = C -> ( ch <-> th ) ) $.
    ceqsex8v.12 $e |- ( w = D -> ( th <-> ta ) ) $.
    ceqsex8v.13 $e |- ( v = E -> ( ta <-> et ) ) $.
    ceqsex8v.14 $e |- ( u = F -> ( et <-> ze ) ) $.
    ceqsex8v.15 $e |- ( t = G -> ( ze <-> si ) ) $.
    ceqsex8v.16 $e |- ( s = H -> ( si <-> rh ) ) $.
    $( Elimination of eight existential quantifiers, using implicit
       substitution. $)
    ceqsex8v $p |- ( E. x E. y E. z E. w E. v E. u E. t E. s
              ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) )
         /\ ( ( v = E /\ u = F ) /\ ( t = G /\ s = H ) ) /\ ph ) <-> rh ) $=
      ( cv wceq wa w3a wex 19.42vv 2exbii bitri 3anass df-3an anbi2i bitr4i
      3bitr4i 3anbi3d 4exbidv ceqsex4v 3bitri ) JVBQVCZKVBRVCZVDZLVBSVCZMVBTVCZ
      VDZVDZNVBUAVCOVBUBVCVDZPVBUCVCUEVBUDVCVDZVDZAVEZUEVFPVFZOVFNVFZMVFLVFZKVF
      JVFWAWDWFWGAVEZUEVFPVFZOVFNVFZVEZMVFLVFZKVFJVFWFWGEVEZUEVFPVFOVFNVFZIWLWQ
      JKWKWPLMWEWMVDZUEVFPVFZOVFNVFZWEWOVDZWKWPXBWEWNVDZOVFNVFXCXAXDNOWEWMPUEVG
      VHWEWNNOVGVIWJXANOWIWTPUEWIWEWHAVDZVDWTWEWHAVJWMXEWEWFWGAVKVLVMVHVHWAWDWO
      VKVNVHVHWOWFWGBVEZUEVFPVFOVFNVFWFWGCVEZUEVFPVFOVFNVFWFWGDVEZUEVFPVFOVFNVF
      WSJKLMQRSTUFUGUHUIVSWMXFNOPUEVSABWFWGUNVOVPVTXFXGNOPUEVTBCWFWGUOVOVPWBXGX
      HNOPUEWBCDWFWGUPVOVPWCXHWRNOPUEWCDEWFWGUQVOVPVQEFGHINOPUEUAUBUCUDUJUKULUM
      URUSUTVAVQVR $.
      $( [23-Sep-2011] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex.1 $e |- A e. _V $.
    gencbvex.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    gencbvex $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex excom wb anbi12d bicomd eqcoms ceqsexv exbii 19.41v
      pm3.27 eqcom biimpi adantl eximi sylbi adantr ancri impbii bitri 3bitr3i
      ) FLZGMZDBNZNZFOZEOUREOZFOCANZEOUQFOUREFPUSVAEUQVAFGHUQVAQGUOGUOMZVAUQVBC
      DABJIRSTUAUBUTUQFUTUPEOZUQNZUQUPUQEUCVDUQVCUQUDUQVCDVCBDCVBNZEOVCKVEUPEVB
      UPCVBUPGUOUEUFUGUHUIUJUKULUMUBUN $.
      $( [8-Jun-2011] $) $( [17-May-1996] $)

    $( Change of bound variable using implicit substitution. $)
    gencbvexOLD $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex excom wb anbi12d bicomd eqcoms ceqsexv exbii anass
      pm5.32i ancom eqcom anbi1i 3bitr3i 19.41v 3bitri 3bitr4ri ) FLZGMZDBNZNZF
      OZEOUOEOZFOCANZEOUNFOUOEFPUPUREUNURFGHUNURQGULGULMZURUNUSCDABJIRSTUAUBUQU
      NFUMEOZDNZBNUTUNNUNUQUTDBUCDVABDCUSNZEOUMDNZEOVAKVBVCEUSCNUSDNVBVCUSCDJUD
      USCUEUSUMDGULUFUGUHUBUMDEUIUJUGUMUNEUIUKUBUH $.
      $( [17-May-1996] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbvex2.1 $e |- A e. _V $.
    gencbvex2.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbvex2.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbvex2.4 $e |- ( th -> E. x ( ch /\ A = y ) ) $.
    $( Restatement of ~ gencbvex with weaker hypotheses.  (Contributed by
       Jeffrey Hankins, 6-Dec-2006.) $)
    gencbvex2 $p |- ( E. x ( ch /\ ph ) <-> E. y ( th /\ ps ) ) $=
      ( cv wceq wa wex biimpac 19.23aiv impbii gencbvex ) ABCDEFGHIJDCGFLMZNZEO
      KUADETCDJPQRS $.
      $( [7-Dec-2006] $) $( [6-Dec-2006] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x th $.  $d y ch $.  $d y A $.
    gencbval.1 $e |- A e. _V $.
    gencbval.2 $e |- ( A = y -> ( ph <-> ps ) ) $.
    gencbval.3 $e |- ( A = y -> ( ch <-> th ) ) $.
    gencbval.4 $e |- ( th <-> E. x ( ch /\ A = y ) ) $.
    $( Change of bound variable using implicit substitution. $)
    gencbval $p |- ( A. x ( ch -> ph ) <-> A. y ( th -> ps ) ) $=
      ( wi wal wn wa wex cv wceq notbid gencbvex exanali 3bitr3i con4bii ) CALE
      MZDBLFMZCANZOEPDBNZOFPUDNUENUFUGCDEFGHGFQRABISJKTCAEUADBFUAUBUC $.
      $( [17-May-1996] $)
  $}

  ${
    $d x A $.
    vtoclf.1 $e |- ( ps -> A. x ps ) $.
    vtoclf.2 $e |- A e. _V $.
    vtoclf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclf.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable.  This is a
       generalization of ~ chvar . $)
    vtoclf $p |- ps $=
      ( cv wceq wex wi isseti biimpd eximi ax-mp 19.36i mpg ) ABCABCECIDJZCKABL
      ZCKCDFMSTCSABGNOPQHR $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ps $.
    vtocl.1 $e |- A e. _V $.
    vtocl.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl.3 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl $p |- ps $=
      ( ax-17 vtoclf ) ABCDBCHEFGI $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    vtocl2.1 $e |- A e. _V $.
    vtocl2.2 $e |- B e. _V $.
    vtocl2.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    vtocl2.4 $e |- ph $.
    $( Implicit substitution of classes for set variables.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    vtocl2 $p |- ps $=
      ( wal wi wex cv wceq isseti wa eeanv biimpd 2eximi sylbir mp2an 19.36v
      exbii mpbi 19.36aiv ax-gen mpg ) ADKZBCUIBCABLZDMZCMZUIBLZCMCNEOZCMZDNFOZ
      DMZULCEGPDFHPUOUQQUNUPQZDMCMULUNUPCDRURUJCDURABISTUAUBUKUMCABDUCUDUEUFADJ
      UGUH $.
      $( [8-Jun-2011] $) $( [26-Jul-1995] $)

    $( Implicit substitution of classes for set variables. $)
    vtocl2OLD $p |- ps $=
      ( wal wi wex cv wceq isseti wa eeanv biimpd 2eximi sylbir mp2an 19.36v
      exbii bitri mpbi ax-gen mpg ) ADKZBCABLZDMZCMZUICKBLZCNEOZCMZDNFOZDMZULCE
      GPDFHPUOUQQUNUPQZDMCMULUNUPCDRURUJCDURABISTUAUBULUIBLZCMUMUKUSCABDUCUDUIB
      CUCUEUFADJUGUH $.
      $( [26-Jul-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    vtocl3.1 $e |- A e. _V $.
    vtocl3.2 $e |- B e. _V $.
    vtocl3.3 $e |- C e. _V $.
    vtocl3.4 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    vtocl3.5 $e |- ph $.
    $( Implicit substitution of classes for set variables.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    vtocl3 $p |- ps $=
      ( wal wi wex cv wceq isseti w3a eeeanv biimpd eximi 2eximi sylbir mp3an
      19.36v 2exbii mpbi exbii 19.36aiv gen2 mpg ) AENZDNZBCUOBCUNBOZDPZCPZUOBO
      ZCPABOZEPZDPCPZURCQFRZCPZDQGRZDPZEQHRZEPZVBCFISDGJSEHKSVDVFVHTVCVEVGTZEPZ
      DPCPVBVCVEVGCDEUAVJVACDVIUTEVIABLUBUCUDUEUFVAUPCDABEUGUHUIUQUSCUNBDUGUJUI
      UKADEMULUM $.
      $( [8-Jun-2011] $) $( [3-Jun-1995] $)

    $( Implicit substitution of classes for set variables. $)
    vtocl3OLD $p |- ps $=
      ( wal wi wex cv wceq isseti w3a eeeanv biimpd eximi 2eximi sylbir mp3an
      19.36v exbii bitri mpbi gen2 mpg ) AENZDNZBCABOZEPZDPZCPZUNCNBOZCQFRZCPZD
      QGRZDPZEQHRZEPZURCFISDGJSEHKSVAVCVETUTVBVDTZEPZDPCPURUTVBVDCDEUAVGUPCDVFU
      OEVFABLUBUCUDUEUFURUNBOZCPUSUQVHCUQUMBOZDPVHUPVIDABEUGUHUMBDUGUIUHUNBCUGU
      IUJADEMUKUL $.
      $( [3-Jun-1995] $)
  $}

  ${
    $d x A $.  $d x ch $.  $d x th $.
    vtoclb.1 $e |- A e. _V $.
    vtoclb.2 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclb.3 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclb.4 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclb $p |- ( ch <-> th ) $=
      ( wb cv wceq bibi12d vtocl ) ABKCDKEFGELFMACBDHINJO $.
      $( [23-Dec-1993] $)
  $}

  ${
    $d y A z $.  $d x z $.  $d y z $.
    vtoclgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgf.2 $e |- ( ps -> A. x ps ) $.
    vtoclgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgf.4 $e |- ph $.
    $( Implicit substitution of a class for a set variable, with bound-variable
       hypotheses in place of distinct variable restrictions.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       11-Jul-2011.) $)
    vtoclgf $p |- ( A e. B -> ps ) $=
      ( vz wcel cvv elisset cv wceq wex isset hblem hbeleq ax-17 eqeq1 cbvex
      bitri mpbii 19.23ai sylbi syl ) EFLEMLZBEFNUICOZEPZCQZBUIKOZEPZKQULKERUNU
      KKCCKECDKEGSTUKKUAUMUJEUBUCUDUKBCHUKABJIUEUFUGUH $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    vtoclg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclg.2 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclg $p |- ( A e. B -> ps ) $=
      ( vy cv wcel ax-17 vtoclgf ) ABCHDEHIDJCKBCKFGL $.
      $( [17-Apr-1995] $)
  $}

  ${
    $d x A $.  $d x ch $.  $d x th $.
    vtoclbg.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    vtoclbg.2 $e |- ( x = A -> ( ps <-> th ) ) $.
    vtoclbg.3 $e |- ( ph <-> ps ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclbg $p |- ( A e. B -> ( ch <-> th ) ) $=
      ( wb cv wceq bibi12d vtoclg ) ABKCDKEFGELFMACBDHINJO $.
      $( [29-Apr-1994] $)
  $}

  ${
    $d x A $.  $d y z A $.  $d y z B $.  $d z x $.
    vtocl2gf.1 $e |- ( ps -> A. x ps ) $.
    vtocl2gf.2 $e |- ( ch -> A. y ch ) $.
    vtocl2gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2gf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2gf.5 $e |- ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtocl2gf $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( vz cvv wcel wi cv ax-17 hbim wceq imbi2d vtoclgf impcom elisset sylan
      ) FPQZGIQZCFHQUIUHCUHBRUHCREOGIOSZGQETUHCEUHETKUAESGUBBCUHMUCABDOFPUJFQDT
      JLNUDUDUEFHUFUG $.
      $( [25-Apr-1995] $)
  $}

  ${
    $d x A $.  $d y A $.  $d y B $.  $d x ps $.  $d y ch $.
    vtocl2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2g.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2g.3 $e |- ph $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2g $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( ax-17 vtocl2gf ) ABCDEFGHIBDMCEMJKLN $.
      $( [25-Apr-1995] $)
  $}

  ${
    $d y A $.  $d x B z $.
    vtoclgaf.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgaf.2 $e |- ( ps -> A. x ps ) $.
    vtoclgaf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgaf.4 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       8-Jun-2011.) $)
    vtoclgaf $p |- ( A e. B -> ps ) $=
      ( vz wcel cv wi ax-17 hbel hbim wceq eleq1 imbi12d vtoclgf pm2.43i ) EFLZ
      BCMZFLZANUCBNCDEFGUCBCCDKEFGKMFLCOPHQUDERUEUCABUDEFSITJUAUB $.
      $( [11-Jul-2011] $) $( [17-Feb-2006] $)
  $}

  ${
    $d y A $.  $d x y $.  $d x y B $.
    vtoclgafOLD.1 $e |- ( y e. A -> A. x y e. A ) $.
    vtoclgafOLD.2 $e |- ( ps -> A. x ps ) $.
    vtoclgafOLD.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclgafOLD.4 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclgafOLD $p |- ( A e. B -> ps ) $=
      ( wcel cv wi ax-17 hbel hbim wceq eleq1 imbi12d vtoclgf pm2.43i ) EFKZBCL
      ZFKZAMUBBMCDEFGUBBCCDDEFGDLFKCNOHPUCEQUDUBABUCEFRISJTUA $.
      $( [18-Feb-2006] $) $( [17-Feb-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ps $.
    vtoclga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclga.2 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclga $p |- ( A e. B -> ps ) $=
      ( vy cv wcel ax-17 vtoclgaf ) ABCHDEHIDJCKBCKFGL $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x ps $.  $d y ch $.
    vtocl2ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl2ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl2ga.3 $e |- ( ( x e. C /\ y e. D ) -> ph ) $.
    $( Implicit substitution of 2 classes for 2 set variables. $)
    vtocl2ga $p |- ( ( A e. C /\ B e. D ) -> ch ) $=
      ( wcel wi cv wceq imbi2d ex vtoclga com12 impcom ) GIMFHMZCUBBNUBCNEGIEOZ
      GPBCUBKQUBUCIMZBUDANUDBNDFHDOZFPABUDJQUEHMUDALRSTSUA $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d z C $.  $d x y z D $.  $d x y z R $.
    $d x y z S $.  $d x ps $.  $d y ch $.  $d z th $.
    vtocl3ga.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtocl3ga.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    vtocl3ga.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    vtocl3ga.4 $e |- ( ( x e. D /\ y e. R /\ z e. S ) -> ph ) $.
    $( Implicit substitution of 3 classes for 3 set variables. $)
    vtocl3ga $p |- ( ( A e. D /\ B e. R /\ C e. S ) -> th ) $=
      ( wcel wi cv wceq imbi2d wa 3expib vtoclga com12 vtocl2ga ex com3r 3imp
      ) HKRZILRZJMRZDULUMUKDULUMUKDSZUKBSUKCSUNFGIJLMFTZIUABCUKOUBGTZJUACDUKPUB
      UKUOLRZUPMRZUCZBUSASUSBSEHKETZHUAABUSNUBUTKRUQURAQUDUEUFUGUHUIUJ $.
      $( [20-Aug-1995] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocleg.1 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocleg $p |- ( A e. B -> ph ) $=
      ( wcel cv wceq wex elex 19.23aiv syl ) CDFBGCHZBIABCDJMABEKL $.
      $( [10-Jan-2004] $)
  $}

  ${
    $d x A $.
    $( Implicit substitution of a class for a set variable.  (Closed theorem
       version of ~ vtoclef .)  (The proof was shortened by Andrew Salmon,
       8-Jun-2011.) $)
    vtoclegft $p |- ( ( A e. B /\ A. x ( ph -> A. x ph ) /\
                   A. x ( x = A -> ph ) ) -> ph ) $=
      ( wcel wal wi cv wceq wex elex pm2.27 19.9t syl9 exim syl7 syl 3imp ) CDE
      ZAABFGBFZBHCIZAGBFZASUABJZTUBAGGBCDKUCTUCABJZGZAUBUCUEUDTAUCUDLABMNUAABOP
      QR $.
      $( [8-Jun-2011] $) $( [7-Nov-2005] $)

    $( Implicit substitution of a class for a set variable.  (Closed theorem
       version of ~ vtoclef .) $)
    vtoclegftOLD $p |- ( ( A e. B /\ A. x ( ph -> A. x ph ) /\
                   A. x ( x = A -> ph ) ) -> ph ) $=
      ( wcel wal wi cv wceq wa wex wb 19.23t adantl elex pm2.27 syl adantr
      sylbid 3impia ) CDEZAABFGBFZBHCIZAGBFZAUAUBJUDUCBKZAGZAUBUDUFLUAUCABMNUAU
      FAGZUBUAUEUGBCDOUEAPQRST $.
      $( [9-Nov-2005] $) $( [7-Nov-2005] $)
  $}

  ${
    $d x A $.
    vtoclef.1 $e |- ( ph -> A. x ph ) $.
    vtoclef.2 $e |- A e. _V $.
    vtoclef.3 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclef $p |- ph $=
      ( cv wceq wex isseti 19.23ai ax-mp ) BGCHZBIABCEJMABDFKL $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ph $.
    vtocle.1 $e |- A e. _V $.
    vtocle.2 $e |- ( x = A -> ph ) $.
    $( Implicit substitution of a class for a set variable. $)
    vtocle $p |- ph $=
      ( cvv wcel vtocleg ax-mp ) CFGADABCFEHI $.
      $( [9-Sep-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    vtoclri.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    vtoclri.2 $e |- A. x e. B ph $.
    $( Implicit substitution of a class for a set variable. $)
    vtoclri $p |- ( A e. B -> ps ) $=
      ( rspec vtoclga ) ABCDEFACEGHI $.
      $( [21-Nov-1994] $)
  $}

  ${
    $d y A z $.  $d x z $.
    cla4gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    cla4gf.2 $e |- ( ps -> A. x ps ) $.
    cla4gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition.  Compare Theorem
       7.3 of [Quine] p. 44.  (Unnecessary distinct variable restrictions were
       removed by Andrew Salmon, 12-Aug-2011.) $)
    cla4gf $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( vz wcel cvv wal wi elisset wex cv wceq isset hblem hbeleq ax-17 eqeq1
      cbvex bitri biimpd eximi sylbi 19.36 sylib syl ) EFKELKZACMBNZEFOULABNZCP
      ZUMULCQZERZCPZUOULJQZERZJPURJESUTUQJCCJECDJEGTUAUQJUBUSUPEUCUDUEUQUNCUQAB
      IUFUGUHABCHUIUJUK $.
      $( [12-Aug-2011] $) $( [2-Feb-1997] $)

    $d y A $.

    $( Existential specialization, using implicit substitition. $)
    cla4egf $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( wcel wn wal wex hbn cv wceq notbid cla4gf con2d df-ex syl6ibr ) EFJZBAK
      ZCLZKACMUBUDBUCBKCDEFGBCHNCOEPABIQRSACTUA $.
      $( [12-Aug-2011] $) $( [2-Feb-1997] $)

    $d x y $.
    $( Rule of specialization, using implicit substitition.  Compare Theorem
       7.3 of [Quine] p. 44. $)
    cla4gfOLD $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( wcel cvv wal wi elisset wex cv wceq isset hbeleq ax-17 eqeq1 cbvex
      bitri biimpd eximi sylbi 19.36 sylib syl ) EFJEKJZACLBMZEFNUJABMZCOZUKUJC
      PZEQZCOZUMUJDPZEQZDOUPDERURUODCCDEGSUODTUQUNEUAUBUCUOULCUOABIUDUEUFABCHUG
      UHUI $.
      $( [2-Feb-1997] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    cla4gv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition.  Compare Theorem
       7.3 of [Quine] p. 44. $)
    cla4gv $p |- ( A e. B -> ( A. x ph -> ps ) ) $=
      ( vy cv wcel ax-17 cla4gf ) ABCGDEGHDICJBCJFK $.
      $( [22-Jun-1994] $)

    $( Existential specialization, using implicit substitition. $)
    cla4egv $p |- ( A e. B -> ( ps -> E. x ph ) ) $=
      ( vy cv wcel ax-17 cla4egf ) ABCGDEGHDICJBCJFK $.
      $( [14-Aug-1994] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla42egv.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with 2 quantifiers, using implicit
       substitution. $)
    cla42egv $p |- ( ( A e. C /\ B e. D ) -> ( ps -> E. x E. y ph ) ) $=
      ( cv wceq wa wex wcel biimprcd 2eximdv elex anim12i eeanv sylibr syl5com
      ) BCJEKZDJFKZLZDMCMZADMCMEGNZFHNZLZBUDACDUDABIOPUHUBCMZUCDMZLUEUFUIUGUJCE
      GQDFHQRUBUCCDSTUA $.
      $( [3-Aug-1995] $)

    $( Specialization with 2 quantifiers, using implicit substitution. $)
    cla42gv $p |- ( ( A e. C /\ B e. D ) -> ( A. x A. y ph -> ps ) ) $=
      ( wcel wa wal wn wex cv wceq notbid cla42egv exnal exbii bitr2i syl6ibr
      con4d ) EGJFHJKZBADLZCLZUDBMZAMZDNZCNZUFMZUHUGCDEFGHCOEPDOFPKABIQRUJUEMZC
      NUKUIULCADSTUECSUAUBUC $.
      $( [13-May-2004] $) $( [27-Apr-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    cla43egv.1 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    $( Existential specialization with 3 quantifiers, using implicit
       substitution. $)
    cla43egv $p |- ( ( A e. R /\ B e. S /\ C e. T ) ->
               ( ps -> E. x E. y E. z ph ) ) $=
      ( cv wceq w3a wex wcel biimprcd eximdv 2eximdv elex 3anim123i eeeanv
      sylibr syl5com ) BCMFNZDMGNZEMHNZOZEPZDPCPZAEPZDPCPFIQZGJQZHKQZOZBUJULCDB
      UIAEUIABLRSTUPUFCPZUGDPZUHEPZOUKUMUQUNURUOUSCFIUADGJUAEHKUAUBUFUGUHCDEUCU
      DUE $.
      $( [12-May-2008] $)

    $( Specialization with 3 quantifiers, using implicit substitution. $)
    cla43gv $p |- ( ( A e. R /\ B e. S /\ C e. T ) ->
               ( A. x A. y A. z ph -> ps ) ) $=
      ( wcel w3a wal wn wex cv wceq notbid cla43egv exnal exbii bitri bitr2i
      syl6ibr con4d ) FIMGJMHKMNZBAEOZDOZCOZUHBPZAPZEQZDQZCQZUKPZUMULCDEFGHIJKC
      RFSDRGSERHSNABLTUAUPUJPZCQUQUOURCUOUIPZDQURUNUSDAEUBUCUIDUBUDUCUJCUBUEUFU
      G $.
      $( [15-May-2008] $) $( [12-May-2008] $)
  $}

  ${
    $d x A $.  $d x ps $.
    cla4v.1 $e |- A e. _V $.
    cla4v.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Rule of specialization, using implicit substitition. $)
    cla4v $p |- ( A. x ph -> ps ) $=
      ( cvv wcel wal wi cla4gv ax-mp ) DGHACIBJEABCDGFKL $.
      $( [22-Jun-1994] $)

    $( Existential specialization, using implicit substitition.  (The proof was
       shortened by Eric Schmidt, 22-Dec-2006.) $)
    cla4ev $p |- ( ps -> E. x ph ) $=
      ( cvv wcel wex wi cla4egv ax-mp ) DGHBACIJEABCDGFKL $.
      $( [22-Dec-2006] $) $( [31-Dec-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    cla4e2v.1 $e |- A e. _V $.
    cla4e2v.2 $e |- B e. _V $.
    cla4e2v.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Existential specialization, using implicit substitition. $)
    cla42ev $p |- ( ps -> E. x E. y ph ) $=
      ( cvv wcel wex wi cla42egv mp2an ) EJKFJKBADLCLMGHABCDEFJJINO $.
      $( [3-Aug-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.
    rcla4.1 $e |- ( ps -> A. x ps ) $.
    rcla4.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization, using implicit substitition.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    rcla4 $p |- ( A e. B -> ( A. x e. B ph -> ps ) ) $=
      ( vy wcel cv wi wal wral ax-17 hbim wceq eleq1 imbi12d cla4gf pm2.43a
      df-ral syl5ib ) DEIZCJZEIZAKZCLZBACEMUGUCBUFUCBKCHDEHJDICNUCBCUCCNFOUDDPU
      EUCABUDDEQGRSTACEUAUB $.
      $( [8-Jun-2011] $) $( [19-Apr-2005] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4OLD $p |- ( A e. B -> ( A. x e. B ph -> ps ) ) $=
      ( vy wral wcel cv wi wal df-ral ax-17 hbim wceq eleq1 imbi12d cla4gf
      pm2.43b sylbi com12 ) ACEIZDEJZBUDCKZEJZALZCMZUEBLZACENUIUEBUHUJCHDEHKDJC
      OUEBCUECOFPUFDQUGUEABUFDERGSTUAUBUC $.
      $( [24-May-2005] $) $( [19-Apr-2005] $)

    $( Restricted existential specialization, using implicit substitition. $)
    rcla4e $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( vy wcel wa cv wex wrex ax-17 hban wceq eleq1 anbi12d cla4egf anabsi5
      df-rex sylibr ) DEIZBJZCKZEIZAJZCLZACEMUCBUHUGUDCHDEHKDICNUCBCUCCNFOUEDPU
      FUCABUEDEQGRSTACEUAUB $.
      $( [26-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rcla4v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted specialization, using implicit substitition. $)
    rcla4v $p |- ( A e. B -> ( A. x e. B ph -> ps ) ) $=
      ( ax-17 rcla4 ) ABCDEBCGFH $.
      $( [24-May-2005] $) $( [26-May-1998] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4cv $p |- ( A. x e. B ph -> ( A e. B -> ps ) ) $=
      ( wcel wral rcla4v com12 ) DEGACEHBABCDEFIJ $.
      $( [4-Feb-2006] $) $( [2-Feb-2006] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4va $p |- ( ( A e. B /\ A. x e. B ph ) -> ps ) $=
      ( wcel wral rcla4v imp ) DEGACEHBABCDEFIJ $.
      $( [15-Sep-2005] $) $( [13-Sep-2005] $)

    $( Restricted specialization, using implicit substitition.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    rcla4cva $p |- ( ( A. x e. B ph /\ A e. B ) -> ps ) $=
      ( wcel wral rcla4v impcom ) DEGACEHBABCDEFIJ $.
      $( [8-Jun-2011] $) $( [26-Jul-2006] $)

    $( Restricted specialization, using implicit substitition. $)
    rcla4cvaOLD $p |- ( ( A. x e. B ph /\ A e. B ) -> ps ) $=
      ( wcel wral rcla4va ancoms ) DEGACEHBABCDEFIJ $.
      $( [27-Jul-2006] $) $( [26-Jul-2006] $)

    $( Restricted existential specialization, using implicit substitition. $)
    rcla4ev $p |- ( ( A e. B /\ ps ) -> E. x e. B ph ) $=
      ( ax-17 rcla4e ) ABCDEBCGFH $.
      $( [26-May-1998] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.  $d x ch $.
    rcla4dv.1 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Restricted specialization, using implicit substitition. $)
    rcla4dv $p |- ( ( ph /\ A e. B ) -> ( A. x e. B ps -> ch ) ) $=
      ( wcel wral wi cv wceq wb expcom pm5.74d rcla4v r19.21v syl5ibr pm2.86d
      impcom ) EFHZABDFIZCJUAAUBCUAABJZDFIACJZAUBJUCUDDEFDKELZABCAUEBCMGNOPABDF
      QRST $.
      $( [18-Feb-2007] $) $( [17-Feb-2007] $)

    $( Restricted existential specialization, using implicit substitition.
       (Contributed by FL, 17-Apr-2007.) $)
    rcla4edv $p |- ( ( ph /\ A e. B ) -> ( ch -> E. x e. B ps ) ) $=
      ( wcel wrex wi wa cv wceq wb expcom pm5.74d rcla4ev r19.37av syl ex
      pm2.86d impcom ) EFHZACBDFIZJUCACUDUCACJZAUDJZUCUEKABJZDFIUFUGUEDEFDLEMZA
      BCAUHBCNGOPQABDFRSTUAUB $.
      $( [10-May-2008] $) $( [17-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d x C $.  $d x D $.  $d y B $.  $d y D $.  $d x ch $.
    $d y ps $.
    rcla42v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla42v.2 $e |- ( y = B -> ( ch <-> ps ) ) $.
    $( 2-variable restricted specialization, using implicit substitition. $)
    rcla42v $p |- ( ( A e. C /\ B e. D ) -> ( A. x e. C A. y e. D ph ->
                  ps ) ) $=
      ( wcel wral cv wceq ralbidv rcla4v sylan9 ) FHLAEIMZDHMCEIMZGILBSTDFHDNFO
      ACEIJPQCBEGIKQR $.
      $( [24-May-2005] $) $( [13-Sep-1999] $)

    $( 2-variable restricted existential specialization, using implicit
       substitution. $)
    rcla42ev $p |- ( ( A e. C /\ B e. D /\ ps ) -> E. x e. C E. y e. D ph ) $=
      ( wcel w3a wrex wa rcla4ev anim2i 3impb cv wceq rexbidv syl ) FHLZGILZBMU
      CCEINZOZAEINZDHNUCUDBUFUDBOUEUCCBEGIKPQRUGUEDFHDSFTACEIJUAPUB $.
      $( [13-Feb-2007] $) $( [16-Oct-1999] $)
  $}

  ${
    $d z ps $.  $d x ch $.  $d y th $.  $d x y z A $.  $d y z B $.  $d z C $.
    $d x R $.  $d x y S $.  $d x y z T $.
    rcla43v.1 $e |- ( x = A -> ( ph <-> ch ) ) $.
    rcla43v.2 $e |- ( y = B -> ( ch <-> th ) ) $.
    rcla43v.3 $e |- ( z = C -> ( th <-> ps ) ) $.
    $( 3-variable restricted specialization, using implicit substitition. $)
    rcla43v $p |- ( ( A e. R /\ B e. S /\ C e. T ) ->
                  ( A. x e. R A. y e. S A. z e. T ph -> ps ) ) $=
      ( wcel wral wi wa cv wceq ralbidv rcla42v rcla4v sylan9 3impa ) HKQZILQZJ
      MQZAGMRZFLREKRZBSUHUITULDGMRZUJBUKUMCGMREFHIKLEUAHUBACGMNUCFUAIUBCDGMOUCU
      DDBGJMPUEUFUG $.
      $( [1-Jun-2005] $) $( [10-May-2005] $)
  $}

  ${
    $d x A y z $.  $d x B y z $.
    eqvinc.1 $e |- A e. _V $.
    $( A variable introduction law for class equality.  (The proof was
       shortened by Andrew Salmon, 8-Jun-2011.) $)
    eqvinc $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( wceq cv wa wex wi isseti ax-1 eqtr ex jca eximi ax-mp pm3.43 19.37aiv
      eqtr2 19.23aiv impbii ) BCEZAFZBEZUCCEZGZAHUBUFAUBUDIZUBUEIZGZAHZUBUFIZAH
      UDAHUJABDJUDUIAUDUGUHUDUBKUDUBUEUCBCLMNOPUIUKAUBUDUEQOPRUFUBAUCBCSTUA $.
      $( [8-Jun-2011] $) $( [14-Apr-1995] $)

    $( A variable introduction law for class equality. $)
    eqvincOLD $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( vz vy wceq cvv wcel cv wa wex eleq1 mpbii visset adantl 19.23aiv eqeq2
      anbi2d exbidv eqeq1 eqcom syl6bb anbi1d equvin vtoclb vtoclbg pm5.21nii
      ) BCGZCHIZAJZBGZUKCGZKZALZUIBHIUJDBCHMNUNUJAUMUJULUMUKHIUJAOUKCHMNPQBEJZG
      ZULUKUPGZKZALZUIUOECHUPCBRUPCGZUSUNAVAURUMULUPCUKRSTFJZUPGVBUKGZURKZALUQU
      TFBDVBBUPUAVBBGZVDUSAVEVCULURVEVCBUKGULVBBUKUABUKUBUCUDTFEAUEUFUGUH $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    eqvincf.1 $e |- ( y e. A -> A. x y e. A ) $.
    eqvincf.2 $e |- ( y e. B -> A. x y e. B ) $.
    eqvincf.3 $e |- A e. _V $.
    $( A variable introduction law for class equality, using bound-variable
       hypotheses instead of distinct variable conditions. $)
    eqvincf $p |- ( A = B <-> E. x ( x = A /\ x = B ) ) $=
      ( wceq cv wa wex eqvinc hbeleq hban ax-17 eqeq1 anbi12d cbvex bitri ) CDH
      BIZCHZTDHZJZBKAIZCHZUDDHZJZAKBCDGLUCUGBAUAUBAABCEMABDFMNUGBOTUDHUAUEUBUFT
      UDCPTUDDPQRS $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d x A y $.  $d ph y $.
    alexeq.1 $e |- A e. _V $.
    $( Two ways to express substitution of ` A ` for ` x ` in ` ph ` . $)
    alexeq $p |- ( A. x ( x = A -> ph ) <-> E. x ( x = A /\ ph ) ) $=
      ( vy cv wceq wa wex wi wal eqeq2 anbi1d exbidv imbi1d albidv sb56 vtoclb
      bicomi ) BFZCGZAHZBIZUAAJZBKZTEFZGZAHZBIUGAJZBKUCUEECDUFCGZUHUBBUJUGUAAUF
      CTLZMNUJUIUDBUJUGUAAUKOPABEQRS $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x A y $.  $d ph y $.
    $( Equality implies equivalence with substitution. $)
    ceqex $p |- ( x = A -> ( ph <-> E. x ( x = A /\ ph ) ) ) $=
      ( vy cvv wcel cv wceq wa wex wb 19.8a isset sylibr wi eqeq2 anbi1d
      exbidv bibi2d imbi12d ex wal ax-4 com12 visset alexeq syl5ibr impbid
      vtoclg mpcom ) CEFZBGZCHZAUMAIZBJZKZUMUMBJUKUMBLBCMNULDGZHZAURAIZBJZKZOUM
      UPODCEUQCHZURUMVAUPUQCULPZVBUTUOAVBUSUNBVBURUMAVCQRSTURAUTURAUTUSBLUAURUR
      AOZBUBZAUTVEURAVDBUCUDABUQDUEUFUGUHUIUJ $.
      $( [2-Mar-1995] $)
  $}

  ${
    $d x y A $.
    ceqsexg.1 $e |- ( ps -> A. x ps ) $.
    ceqsexg.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A representation of explicit substitution of a class for a variable,
       inferred from an implicit substitution hypothesis. $)
    ceqsexg $p |- ( A e. B -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( vy wb cv wceq wa wex wcel ax-17 hbe1 hbbi ceqex bibi12d biid vtoclgf )
      AAICJDKZALZCMZBICHDEHJDNCOUDBCUCCPFQUBAUDABACDRGSATUA $.
      $( [13-Oct-2004] $) $( [11-Oct-2004] $)
  $}

  ${
    $d x y A $.  $d x ps $.
    ceqsexgv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of an existential quantifier, using implicit
       substitution. $)
    ceqsexgv $p |- ( A e. B -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( ax-17 ceqsexg ) ABCDEBCGFH $.
      $( [29-Dec-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    ceqsrexv.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Elimination of a restricted existential quantifier, using implicit
       substitution. $)
    ceqsrexv $p |- ( A e. B -> ( E. x e. B ( x = A /\ ph ) <-> ps ) ) $=
      ( wcel cv wceq wa wex wrex eleq1 anbi12d ceqsexgv bianabs df-rex an12
      exbii bitr4i syl5bb ) DEGZCHZDIZUCEGZAJZJZCKZBUDAJZCELZUBUHBUFUBBJCDEUDUE
      UBABUCDEMFNOPUJUEUIJZCKUHUICEQUGUKCUDUEARSTUA $.
      $( [13-May-2004] $) $( [30-Apr-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.  $d x y D $.  $d x ps $.  $d y ch $.
    ceqsrex2v.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ceqsrex2v.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Elimination of a restricted existential quantifier, using implicit
       substitution. $)
    ceqsrex2v $p |- ( ( A e. C /\ B e. D ) ->
      ( E. x e. C E. y e. D ( ( x = A /\ y = B ) /\ ph ) <-> ch ) ) $=
      ( wcel cv wceq wa wrex anbi2d rexbidv ceqsrexv anass rexbii r19.42v
      bitri syl5bb sylan9bb ) FHLZDMFNZEMGNZOAOZEIPZDHPZUHBOZEIPZGILCUFUGUHAOZE
      IPZOZDHPUMUKUOUMDFHUGUNULEIUGABUHJQRSUJUPDHUJUGUNOZEIPUPUIUQEIUGUHATUAUGU
      NEIUBUCUAUDBCEGIKSUE $.
      $( [1-Nov-2005] $) $( [29-Oct-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel2.1 $e |- A e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel2 $p |- ( A e. B <-> A. x ( x = A -> x e. B ) ) $=
      ( cv wceq wcel wi wal eleq1 ceqsalv bicomi ) AEZBFMCGZHAIBCGZNOABDMBCJKL
      $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3g $p |- ( B e. C -> ( A e. B <-> E. x ( x = B /\ A e. x ) ) ) $=
      ( wcel cv wceq wa wex eleq2 ceqsexgv bicomd ) CDEAFZCGBMEZHAIBCEZNOACDMCB
      JKL $.
      $( [15-Aug-2005] $) $( [13-Aug-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel3.1 $e |- B e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel3 $p |- ( A e. B <-> E. x ( x = B /\ A e. x ) ) $=
      ( cvv wcel cv wceq wa wex wb clel3g ax-mp ) CEFBCFAGZCHBNFIAJKDABCELM $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    clel4.1 $e |- B e. _V $.
    $( An alternate definition of class membership when the class is a set. $)
    clel4 $p |- ( A e. B <-> A. x ( x = B -> A e. x ) ) $=
      ( cv wceq wcel wi wal eleq2 ceqsalv bicomi ) AEZCFBMGZHAIBCGZNOACDMCBJKL
      $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.
    $( Membership in a class abstraction, using implicit substitition.  (Closed
       theorem version of ~ elabg .)  (The proof was shortened by Andrew
       Salmon, 8-Jun-2011.) $)
    elabgt $p |- ( ( A e. B /\ A. x ( x = A -> ( ph <-> ps ) ) ) ->
                 ( A e. { x | ph } <-> ps ) ) $=
      ( vy wcel cv wceq cab wb wi wal ax-17 hbab1 hbel hbbi pm5.5 cla4gf imp
      eleq1 abid syl5bbr bibi1d biimpd a2i alimi sylan2 ) DEGZCHZDIZDACJZGZBKZL
      ZCMZUNUKABKZLZCMUIUPUNUOUNCFDEFHDGCNZUMBCCFFDULUSACFOPBCNQUKUNRSTURUOCUKU
      QUNUKUQUNUKAUMBUKUJULGUMAUJDULUAACUBUCUDUEUFUGUH $.
      $( [8-Jun-2011] $) $( [7-Nov-2005] $)

    $( Membership in a class abstraction, using implicit substitition.  (Closed
       theorem version of ~ elabg .) $)
    elabgtOLD $p |- ( ( A e. B /\ A. x ( x = A -> ( ph <-> ps ) ) ) ->
                 ( A e. { x | ph } <-> ps ) ) $=
      ( vy wcel cv wceq cab wb wi wal ax-17 hbab1 hbel hbbi ax-gen vtoclegft
      mp3an2 eleq1 abid syl5rbbr bibi1d biimprd a2i alimi sylan2 ) DEGZCHZDIZDA
      CJZGZBKZLZCMZUNUKABKZLZCMUIUNUNCMLZCMUPUNUSCUMBCCFFDULFHDGCNACFOPBCNQRUNC
      DESTURUOCUKUQUNUKUNUQUKUMABUKUJULGUMAUJDULUAACUBUCUDUEUFUGUH $.
      $( [10-Nov-2005] $) $( [7-Nov-2005] $)
  $}

  ${
    $d ps y $.  $d x A y $.  $d y ph $.
    elabf.1 $e |- ( ps -> A. x ps ) $.
    elabf.2 $e |- A e. _V $.
    elabf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition. $)
    elabf $p |- ( A e. { x | ph } <-> ps ) $=
      ( vy cab wcel wb cv ax-17 hbab1 hbel hbbi wceq eleq1 abid syl5bbr bitr3d
      vtoclef ) DACIZJZBKCDUDBCCHHDUCHLDJCMACHNOEPFCLZDQZAUDBUFUEUCJUDAUEDUCRAC
      STGUAUB $.
      $( [1-Aug-1994] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab.1 $e |- A e. _V $.
    elab.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elab $p |- ( A e. { x | ph } <-> ps ) $=
      ( ax-17 elabf ) ABCDBCGEFH $.
      $( [1-Aug-1994] $)
  $}

  ${
    $d y A $.  $d z ph $.  $d x z $.  $d y z $.
    elabgf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elabgf.2 $e |- ( ps -> A. x ps ) $.
    elabgf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44.  This version has bound-variable
       hypotheses in place of distinct variable restrictions. $)
    elabgf $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vz cv cab wcel wb hbab1 hbel hbbi wceq eleq1 bibi12d abid vtoclgf ) CKZ
      ACLZMZANEUDMZBNCDEFGUFBCCDJEUDGACJOPHQUCERUEUFABUCEUDSITACUAUB $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    elabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, using implicit substitition.  Compare
       Theorem 6.13 of [Quine] p. 44. $)
    elabg $p |- ( A e. B -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wcel ax-17 elabgf ) ABCGDEGHDICJBCJFK $.
      $( [14-Apr-1995] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2g.2 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2g $p |- ( A e. C -> ( A e. B <-> ps ) ) $=
      ( wcel cab elabg eleq2i syl5bb ) DFIDACJZIBDEIABCDFGKENDHLM $.
      $( [12-Sep-2004] $) $( [13-Sep-1995] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab2.1 $e |- A e. _V $.
    elab2.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elab2.3 $e |- B = { x | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elab2 $p |- ( A e. B <-> ps ) $=
      ( cvv wcel wb elab2g ax-mp ) DIJDEJBKFABCDEIGHLM $.
      $( [12-Sep-2004] $) $( [13-Sep-1995] $)
  $}

  ${
    $d y A $.
    elab3gf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elab3gf.2 $e |- ( ps -> A. x ps ) $.
    elab3gf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabgf . $)
    elab3gf $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( wcel cab wb wn elabgf ibi pm2.21 impbid2 ja ) BEFJEACKZJZBLBMTBTBABCDES
      GHINOBTPQABCDEFGHINR $.
      $( [6-Sep-2011] $)
  $}

  ${
    $d x ps $.  $d x y A $.
    elab3g.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabg . $)
    elab3g $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( vy cv wcel ax-17 elab3gf ) ABCGDEGHDICJBCJFK $.
      $( [14-Jun-2011] $) $( [29-Aug-2006] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab3gOLD.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabg .  (The proof was shortened by Andrew Salmon, 14-Jun-2011.) $)
    elab3gOLD $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( wcel cab wb wn elabg ibi pm2.21 impbid2 ja ) BDEGDACHZGZBIBJQBQBABCDPFK
      LBQMNABCDEFKO $.
      $( [14-Jun-2011] $) $( [29-Aug-2006] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab3gOLDOLD.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction, with a weaker antecedent than
       ~ elabg . $)
    elab3gOLDOLD $p |- ( ( ps -> A e. B ) -> ( A e. { x | ph } <-> ps ) ) $=
      ( wcel wi cab elabg ibi wb imim2i ibibr sylibr impbid2 ) BDEGZHZDACIZGZBT
      BABCDSFJKRBTBLZHBTHQUABABCDEFJMBTNOP $.
      $( [30-Aug-2006] $) $( [29-Aug-2006] $)
  $}

  ${
    $d x ps $.  $d x A $.
    elab3.1 $e |- ( ps -> A e. _V ) $.
    elab3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction using implicit substitution. $)
    elab3 $p |- ( A e. { x | ph } <-> ps ) $=
      ( cvv wcel wi cab wb elab3g ax-mp ) BDGHIDACJHBKEABCDGFLM $.
      $( [10-Nov-2000] $)
  $}

  ${
    $d y A $.  $d y B $.
    elrabf.1 $e |- ( y e. A -> A. x y e. A ) $.
    elrabf.2 $e |- ( y e. B -> A. x y e. B ) $.
    elrabf.3 $e |- ( ps -> A. x ps ) $.
    elrabf.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted class abstraction, using implicit
       substitution.  This version has bound-variable hypotheses in place of
       distinct variable restrictions. $)
    elrabf $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( crab wcel cvv wa elisset adantr cv cab hbel hban wceq eleq1 anbi12d
      elabgf df-rab eleq2i syl5bb pm5.21nii ) EACFKZLZEMLZEFLZBNZEUIOULUKBEFOPU
      KECQZFLZANZCRZLUMUJUPUMCDEMGULBCCDDEFGHSITUNEUAUOULABUNEFUBJUCUDUIUQEACFU
      EUFUGUH $.
      $( [11-Jul-2011] $) $( [21-Sep-2003] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $d x y B $.
    elrab.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a restricted class abstraction, using implicit
       substitution. $)
    elrab $p |- ( A e. { x e. B | ph } <-> ( A e. B /\ ps ) ) $=
      ( vy cv wcel ax-17 elrabf ) ABCGDEGHZDICJLEICJBCJFK $.
      $( [21-May-1999] $)

    $( Membership in a restricted class abstraction, using implicit
       substitution. $)
    elrab3 $p |- ( A e. B -> ( A e. { x e. B | ph } <-> ps ) ) $=
      ( crab wcel elrab baib ) DACEGHDEHBABCDEFIJ $.
      $( [6-Oct-2006] $) $( [5-Oct-2006] $)
  $}

  ${
    $d x ps $.  $d x y A $.  $d x y B $.
    elrab2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    elrab2.2 $e |- C = { x e. B | ph } $.
    $( Membership in a class abstraction, using implicit substitution. $)
    elrab2 $p |- ( A e. C <-> ( A e. B /\ ps ) ) $=
      ( wcel crab wa eleq2i elrab bitri ) DFIDACEJZIDEIBKFODHLABCDEGMN $.
      $( [3-Nov-2006] $) $( [2-Nov-2006] $)
  $}

  ${
    $d x z $.  $d y z $.  $d ph z $.  $d ps z $.
    cbvab.1 $e |- ( ph -> A. y ph ) $.
    cbvab.2 $e |- ( ps -> A. x ps ) $.
    cbvab.3 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitution.
       (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    cbvab $p |- { x | ph } = { y | ps } $=
      ( vz cab cv wsbc wcel hbsb wceq sbequ wb equcoms bicomd sbie syl5bbr
      df-clab 3bitr4i eqriv ) HACIZBDIZACHJZKBDUFKZUFUDLUFUELAUGCHBDHCFMCJZUFNB
      DUHKUGABCHDOBADCEDJUHNABABPCDGQRSTSAHCUABHDUAUBUC $.
      $( [11-Jul-2011] $) $( [10-Jul-2011] $)
  $}

  ${
    $d y ph $.  $d x ps $.
    cbvabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables, using implicit substitution. $)
    cbvabv $p |- { x | ph } = { y | ps } $=
      ( ax-17 cbvab ) ABCDADFBCFEG $.
      $( [11-Jul-2011] $) $( [26-May-1999] $)
  $}

  ${
    $d A w z $.  $d y w $.  $d x w $.  $d ph w $.  $d ps w $.
    cbvrab.1 $e |- ( z e. A -> A. x z e. A ) $.
    cbvrab.2 $e |- ( z e. A -> A. y z e. A ) $.
    cbvrab.3 $e |- ( ph -> A. y ph ) $.
    cbvrab.4 $e |- ( ps -> A. x ps ) $.
    cbvrab.5 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted class abstraction,
       using implicit substitution.  This version has bound-variable hypotheses
       in place of distinct variable conditions.  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    cbvrab $p |- { x e. A | ph } = { y e. A | ps } $=
      ( vw cv wcel wa cab crab wsbc ax-17 hblem hbs1 hban wceq eleq1 sbequ12
      anbi12d cbvab hbsb sbequ sbie syl6bb eqtri df-rab 3eqtr4i ) CMZFNZAOZCPZD
      MZFNZBOZDPZACFQBDFQURLMZFNZACVCRZOZLPVBUQVFCLUQLSVDVECCELFGTACLUAUBUOVCUC
      UPVDAVEUOVCFUDACLUEUFUGVFVALDVDVEDDELFHTACLDIUHUBVALSVCUSUCZVDUTVEBVCUSFU
      DVGVEACUSRBALDCUIABCDJKUJUKUFUGULACFUMBDFUMUN $.
      $( [11-Jul-2011] $) $( [26-Sep-2003] $)
  $}

  ${
    $d x y z A $.  $d y ph $.  $d x ps $.
    cbvrabv.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Rule to change the bound variable in a restricted class abstraction,
       using implicit substitution. $)
    cbvrabv $p |- { x e. A | ph } = { y e. A | ps } $=
      ( vz cv wcel ax-17 cbvrab ) ABCDGEGHEIZCJLDJADJBCJFK $.
      $( [11-Jul-2011] $) $( [26-May-1999] $)
  $}

  ${
    $d w y A $.  $d w x z $.  $d x y $.  $d A z $.
    $( Identity used to create closed-form versions of bound-variable
       hypothesis builders for class expressions.  (Unnecessary distinct
       variable restrictions were removed by Andrew Salmon, 11-Jul-2011.) $)
    abidhb $p |- ( A. y ( y e. A -> A. x y e. A ) ->
                 { z | A. x z e. A } = A ) $=
      ( vw cv wcel wal wi cab hba1 ax4 impbid2 abbid wceq eleq1 albidv cbvabv
      abid2 3eqtr3g ax-17 cbvab syl5eq ) BFZDGZUEAHZIZBHZEFZDGZAHZEJZDCFZDGZAHZ
      CJUHUFBJUEBJULDUHUFUEBUGBKUHUFUEUEALUGBLMNUFUKBEUDUIOUEUJAUDUIDPQRBDSTUOU
      KCEUOEUAUKCUAUMUIOUNUJAUMUIDPQUBUC $.
      $( [11-Jul-2011] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y ph $.  $d x y z $.
    hbeqd.1 $e |- ( ph -> A. x ph ) $.
    hbeqd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbeqd.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbeq . $)
    hbeqd $p |- ( ph -> ( A = B -> A. x A = B ) ) $=
      ( vz cv wcel wal cab wceq wi hba1 hbab hbeq a1i 19.21aiv abidhb syl
      eqeq12d albid 3imtr3d ) AIJZDKZBLZIMZUFEKZBLZIMZNZUMBLZDENZUOBLUMUNOABCCU
      IULUHBICUGBPQUKBICUJBPQRSAUIDULEACJZDKZUQBLOZCLUIDNAURCGTBCIDUAUBAUPEKZUS
      BLOZCLULENAUTCHTBCIEUAUBUCZAUMUOBFVAUDUE $.
      $( [9-Dec-2005] $) $( [6-Dec-2005] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y ph $.  $d x y z $.
    hbeld.1 $e |- ( ph -> A. x ph ) $.
    hbeld.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbeld.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbel . $)
    hbeld $p |- ( ph -> ( A e. B -> A. x A e. B ) ) $=
      ( vz cv wcel wal cab wi hba1 hbab hbel a1i wceq 19.21aiv abidhb syl
      eleq12d albid 3imtr3d ) AIJZDKZBLZIMZUFEKZBLZIMZKZUMBLZDEKZUOBLUMUNNABCCU
      IULUHBICUGBOPUKBICUJBOPQRAUIDULEACJZDKZUQBLNZCLUIDSAURCGTBCIDUAUBAUPEKZUS
      BLNZCLULESAUTCHTBCIEUAUBUCZAUMUOBFVAUDUE $.
      $( [19-Nov-2005] $) $( [10-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x z $.  $d x y $.  $d z A $.
    dedhb.1 $e |- ( A = { z | A. x z e. A } -> ( ph <-> ps ) ) $.
    dedhb.2 $e |- ps $.
    $( A deduction theorem for converting the inference
       ` |- ( y e. A -> A. x y e. A ) ` => ` |- ph ` into a closed theorem.
       Use ~ hba1 and ~ hbab to eliminate the hypothesis of the substitution
       instance ` ps ` of the inference.  For converting the inference form
       into a deduction form, ~ abidhb is useful. $)
    dedhb $p |- ( A. y ( y e. A -> A. x y e. A ) -> ph ) $=
      ( cv wcel wal wi cab wceq wb abidhb eqcomd syl mpbiri ) DIFJZTCKLDKZABHUA
      FEIFJCKEMZNABOUAUBFCDEFPQGRS $.
      $( [11-Jul-2011] $) $( [8-Dec-2006] $)
  $}

  ${
    $d x y A $.
    $( Equality has existential uniqueness. $)
    eueq $p |- ( A e. _V <-> E! x x = A ) $=
      ( vy cv wceq wex wa wi wal cvv wcel weu eqtr3 gen2 biantru isset eqeq1
      eu4 3bitr4i ) ADZBEZAFZUBUACDZBEZGTUCEHZCIAIZGBJKUAALUFUBUEACTUCBMNOABPUA
      UDACTUCBQRS $.
      $( [25-Nov-1994] $)
  $}

  ${
    $d x A $.
    eueq1.1 $e |- A e. _V $.
    $( Equality has existential uniqueness. $)
    eueq1 $p |- E! x x = A $=
      ( cvv wcel cv wceq weu eueq mpbi ) BDEAFBGAHCABIJ $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x B $.
    eueq2.1 $e |- A e. _V $.
    eueq2.2 $e |- B e. _V $.
    $( Equality has existential uniqueness (split into 2 cases). $)
    eueq2 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ph /\ x = B ) ) $=
      ( cv wceq wa wn wo weu euorv notnot1 eueq1 euanv biimpri mpan2 sylanc
      bianfd orbi2d orcom syl5bb eubidv mpbid mpdan id orbi1d pm2.61i ) AABGZCH
      ZIZAJZUJDHZIZKZBLZAUMULKZBLZUQUMJULBLZUSAUMULBMANZAUKBLZUTBCEOUTAVBIAUKBP
      QRSAURUPBAULUMKUPURAUMUOULAUMUNVATUAUMULUBUCUDUEUMAUOKZBLZUQUMUOBLZVDUMUN
      BLZVEBDFOVEUMVFIUMUNBPQRAUOBMUFUMVCUPBUMAULUOUMAUKUMUGTUHUDUEUI $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x ph $.  $d x ps $.  $d x A $.  $d x B $.  $d x C $.
    eueq3.1 $e |- A e. _V $.
    eueq3.2 $e |- B e. _V $.
    eueq3.3 $e |- C e. _V $.
    eueq3.4 $e |- -. ( ph /\ ps ) $.
    $( Equality has existential uniqueness (split into 3 cases). $)
    eueq3 $p |- E! x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( cv wceq wa wo wn w3o weu euorv pm2.45 wi imnan mpbir con2i jaoi eueq1
      euanv biimpri mpan2 sylanc notnot1 orcs bianfd orbi12d orbi2d orcom
      3orass 3bitr4g eubidv mpbid pm2.46 olcs orbi1d df-3or syl6bbr mpdan
      syl6bb orass ecase3 ) ABACKZDLZMZABNZOZVIELZMZBVIFLZMZPZCQZAVMBNZVKNZCQZV
      SVTOVKCQZWBAVTVKCRVTAVMAOBABSZABABOZTABMOJABUAUBZUCZUDUCAVJCQZWCCDGUEWCAW
      HMAVJCUFUGUHUIAWAVRCAVKVTNVKVOVQNZNZWAVRAVTWIVKAVMVOBVQAVMVNABVMOZVLUJZUK
      ULABVPWFULUMUNVTVKUOVKVOVQUPZUQURUSBAVMNZVQNZCQZVSWNOVQCQZWPBWNVQCRWNBAWE
      VMWFABUTZUDUCBVPCQZWQCFIUEWQBWSMBVPCUFUGUHUIBWOVRCBWOVKVONZVQNVRBWNWTVQBA
      VKVMVOBAVJWGULBVMVNABWKWLVAULUMVBVKVOVQVCVDURUSVMVLVONZCQZVSVMVOCQZXBVMVN
      CQZXCCEHUEXCVMXDMVMVNCUFUGUHVLVOCRVEVMXAVRCVMABVONZNWJXAVRVMAVKXEWIVMAVJW
      DULVMXEVQVONWIVMBVQVOVMBVPWRULVBVQVOUOVFUMABVOVGWMUQURUSVH $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x A $.
    $( There is at most one set equal to a class. $)
    moeq $p |- E* x x = A $=
      ( cv wceq wmo wex weu wi cvv wcel isset eueq bitr3i biimpi df-mo mpbir )
      ACBDZAEQAFZQAGZHRSRBIJSABKABLMNQAOP $.
      $( [8-Mar-1995] $)
  $}

  ${
    $d x y ph $.  $d x y ps $.  $d x y A $.  $d x y B $.  $d x y C $.
    moeq3.1 $e |- B e. _V $.
    moeq3.2 $e |- C e. _V $.
    moeq3.3 $e |- -. ( ph /\ ps ) $.
    $( "At most one" property of equality (split into 3 cases).  (The first 2
       hypotheses could be eliminated with longer proof.) $)
    moeq3 $p |- E* x ( ( ph /\ x = A ) \/ ( -. ( ph \/ ps ) /\ x = B )
                \/ ( ps /\ x = C ) ) $=
      ( vy cvv wcel cv wceq wa wo wn w3o wmo weu eqeq2 anbi2d biidd 3orbi123d
      eubidv visset eueq3 vtoclg eumo syl wi wal pm2.21 eleq1 mpbii syl5
      anim2d orim1d 3orass 3imtr4g 19.21aiv euimmo mpi pm2.61i ) DKLZACMZDNZOZA
      BPQVFENOZBVFFNOZRZCSZVEVKCTZVLAVFJMZNZOZVIVJRZCTZVMJDKVNDNZVQVKCVSVPVHVIV
      IVJVJVSVOVGAVNDVFUAUBVSVIUCVSVJUCUDUEABCVNEFJUFGHIUGZUHVKCUIUJVEQZVRVLVTW
      AVKVQUKZCULVRVLUKWAWBCWAVHVIVJPZPVPWCPVKVQWAVHVPWCWAVGVOAWAVEVOVGVEVOUMVG
      VFKLVECUFVFDKUNUOUPUQURVHVIVJUSVPVIVJUSUTVAVKVQCVBUJVCVD $.
      $( [23-Apr-1995] $)
  $}

  ${
    $d x y A $.
    mosub.1 $e |- E* x ph $.
    $( "At most one" remains true after substitution. $)
    mosub $p |- E* x E. y ( y = A /\ ph ) $=
      ( cv wceq wmo wal wa wex moeq ax-gen moexexv mp2an ) CFDGZCHABHZCIPAJCKBH
      CDLQCEMPACBNO $.
      $( [9-Mar-1995] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Theorem for inferring "at most one." $)
    mo2icl $p |- ( A. x ( ph -> x = A ) -> E* x ph ) $=
      ( vy cvv wcel cv wceq wi wal wmo eqeq2 imbi2d albidv imbi1d wex 19.8a
      ax-17 mo2 sylibr vtoclg wn visset eleq1 mpbii imim2i con3d com12 alimdv
      alnex exmo ori sylbi syl6 pm2.61i ) CEFZABGZCHZIZBJZABKZIZAUQDGZHZIZBJZVA
      IVBDCEVCCHZVFUTVAVGVEUSBVGVDURAVCCUQLMNOVFVFDPVAVFDQABDADRSTUAUPUBZUTAUBZ
      BJZVAVHUSVIBUSVHVIUSAUPURUPAURUQEFUPBUCUQCEUDUEUFUGUHUIVJABPZUBVAABUJVKVA
      ABUKULUMUNUO $.
      $( [17-Oct-1996] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x y ps $.
    moi2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Consequence of "at most one." $)
    moi2 $p |- ( ( ( A e. B /\ E* x ph ) /\ ( ph /\ ps ) ) -> x = A ) $=
      ( vy wcel wmo wa cv wceq wsbc wi wal wex wb visset eqvinc hbs1 ax-17
      hbbi sbequ12 bicomd sylan9bb 19.23ai sylbi anbi2d eqeq2 imbi12d cla4gv
      a4sd mo4f syl5ib imp31 ) DEHZACIZABJZCKZDLZUPAACGKZMZJZUSVALZNZGOZCOURUTN
      ZUQUPVFVGCVEVGGDEVADLZVCURVDUTVHVBBAVHVDUTJZCPVBBQZCVADGRSVIVJCVBBCACGTZB
      CUAUBVDVBAUTBVDAVBACGUCZUDFUEUFUGUHVADUSUIUJUKULAVBCGVKVLUMUNUO $.
      $( [29-Jun-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.  $d y ph $.  $d x y ps $.
    moi.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    moi.2 $e |- ( x = B -> ( ph <-> ch ) ) $.
    $( MAY BE REVISED - is there a way to shorten w/ moi2? $)
    $( Equality implied by "at most one." $)
    moi $p |- ( ( ( A e. C /\ B e. D ) /\ E* x ph /\ ( ps /\ ch ) ) ->
              A = B ) $=
      ( vy wcel wa wmo wceq cv wsbc wi wal ax-17 hbs1 hban hbim hbal anbi1d
      eqeq1 imbi12d albidv cla4gf wex wb visset eqvinc hbbi sbequ12 bicomd
      sylan9bb 19.23ai sylbi anbi2d eqeq2 cla4gv sylan9 mo4f syl5ib 3imp ) EGLZ
      FHLZMZADNZBCMZEFOZVIAADKPZQZMZDPZVMOZRZKSZDSZVKVLRZVJVGVTBVNMZEVMOZRZKSZV
      HWAVSWEDKEGVMELDTWDDKWBWCDBVNDBDTADKUAZUBWCDTUCUDVPEOZVRWDKWGVOWBVQWCWGAB
      VNIUEVPEVMUFUGUHUIWDWAKFHVMFOZWBVKWCVLWHVNCBWHVQVPFOZMZDUJVNCUKZDVMFKULUM
      WJWKDVNCDWFCDTUNVQVNAWICVQAVNADKUOZUPJUQURUSUTVMFEVAUGVBVCAVNDKWFWLVDVEVF
      $.
      $( [20-Feb-2006] $) $( [18-Feb-2006] $)
  $}

  ${
    $d x ph $.  $d x A $.
    euxfr2.1 $e |- A e. _V $.
    euxfr2.2 $e |- E* y x = A $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    euxfr2 $p |- ( E! x E. y ( x = A /\ ph ) <-> E! y ph ) $=
      ( cv wceq wa wex weu wmo wi 2euswap moani ancom mobii mpbi mpg moeq
      impbii biidd ceqsexv eubii bitri ) BGDHZAIZCJBKZUGBJZCKZACKUHUJUGCLZUHUJM
      BUGBCNAUFIZCLUKUFACFOULUGCAUFPZQRSUGBLZUJUHMCUGCBNULBLUNUFABBDTOULUGBUMQR
      SUAUIACAABDEUFAUBUCUDUE $.
      $( [15-Nov-2004] $) $( [14-Nov-2004] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.
    euxfr.1 $e |- A e. _V $.
    euxfr.2 $e |- E! y x = A $.
    euxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    euxfr $p |- ( E! x ph <-> E! y ps ) $=
      ( weu cv wceq wa wex euex ax-mp biantrur 19.41v pm5.32i exbii 3bitr2i
      eubii eumoi euxfr2 bitri ) ACICJEKZBLZDMZCIBDIAUGCAUEDMZALUEALZDMUGUHAUED
      IUHGUEDNOPUEADQUIUFDUEABHRSTUABCDEFUEDGUBUCUD $.
      $( [19-Nov-2004] $) $( [14-Nov-2004] $)
  $}

  ${
    $d y z w ph $.  $d x z ps $.  $d y z w A $.  $d x z B $.  $d x y w $.
    euind.0 $e |- B e. _V $.
    euind.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    euind.2 $e |- ( x = y -> A = B ) $.
    $( Existential uniqueness via an indirect equality. $)
    euind $p |- ( ( A. x A. y ( ( ph /\ ps ) -> A = B ) /\ E. x ph )
                 -> E! z A. x ( ph -> z = A ) ) $=
      ( vw cv wceq wi wal weu wex wa eqeq1 imbi2d albidv eu4 wb eqeq2 imim2i
      bi2 an23 ancom anbi1i 3bitri imbi1i impexp 3bitr3i sylib syl 2alimi
      19.23v albii 19.21v bitri eximdv cbvexv isseti biantrur exbii 19.41v
      excom bitr3i syl5ib imp biimpi com12 19.26 prth pm4.24 eqtr3 imim12i
      alimi sylbir syl5 19.21aivv adantl sylanbrc ) AELZFMZNZCOZEPWGEQZWGAKLZFM
      ZNZCOZRZWDWIMZNZKOEOZABRZFGMZNZDOCOZACQZRWGWLEKWNWFWKCWNWEWJAWDWIFSTUAUBW
      TXAWHWTWDGMZBRZDQZEQZWHXAWTXDWGEWTXCWFNZDOZCOZXDWGNZWSXFCDWSWQWEXBUCZNZXF
      WRXJWQFGWDUDUEXKWQXBWENZNZXFXJXLWQWEXBUFUEWQXBRZWENXCARZWENXMXFXNXOWEXNAX
      BRZBRXBARZBRXOABXBUGXPXQBAXBUHUIXBABUGUJUKWQXBWEULXCAWEULUMUNUOUPXHXDWFNZ
      COXIXGXRCXCWFDUQURXDWFCUSUTUNVAXABDQXBEQZBRZDQZXEABCDIVBBXTDXSBEGHVCVDVEY
      AXCEQZDQXEYBXTDXBBEVFVEXCDEVGVHUJVIVJXAWPWTXAWOEKXAAWNNZCOZWNWMYDXAWNYDXA
      WNNAWNCUQVKVLWMWFWKRZCOYDWFWKCVMYEYCCYEAARZWEWJRZNYCAWEAWJVNAYFYGWNAYFAVO
      VKWDWIFVPVQUOVRVSVTWAWBWC $.
      $( [11-Oct-2010] $)
  $}

  $( Restricted unique existence implies restricted existence. $)
  reurex $p |- ( E! x e. A ph -> E. x e. A ph ) $=
    ( cv wcel wa weu wex wreu wrex euex df-reu df-rex 3imtr4i ) BDCEAFZBGOBHABC
    IABCJOBKABCLABCMN $.
    $( [19-Aug-1999] $)

  $( Restricted uniqueness in terms of "at most one." $)
  reu5 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\ E* x ( x e. A /\ ph ) ) ) $=
    ( cv wcel wa weu wex wmo wreu wrex eu5 df-reu df-rex anbi1i 3bitr4i ) BDCEA
    FZBGQBHZQBIZFABCJABCKZSFQBLABCMTRSABCNOP $.
    $( [23-May-1999] $)

  ${
    $d x y A $.  $d y ph $.
    $( A way to express restricted uniqueness. $)
    reu2 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
               A. x e. A A. y e. A ( ( ph /\ [ y / x ] ph ) -> x = y ) ) ) $=
      ( cv wcel wa weu wex wsbc wceq wi wal wreu wrex wral ax-17 eu2 df-reu
      df-rex df-ral 19.21v hbs1 hban eleq1 sbequ12 anbi12d sbie anbi2i an4
      bitri imbi1i impexp 3bitri albii imbi2i 3bitr4i bitr4i anbi12i ) BEZDFZAG
      ZBHVBBIZVBVBBCEZJZGZUTVDKZLZCMZBMZGABDNABDOZAABVDJZGZVGLZCDPZBDPZGVBBCVBC
      QRABDSVKVCVPVJABDTVPVAVOLZBMVJVOBDUAVIVQBVAVDDFZVNLZLZCMVAVSCMZLVIVQVAVSC
      UBVHVTCVHVAVRGZVMGZVGLWBVNLVTVFWCVGVFVBVRVLGZGWCVEWDVBVBWDBCVRVLBVRBQABCU
      CUDVGVAVRAVLUTVDDUEABCUFUGUHUIVAAVRVLUJUKULWBVMVGUMVAVRVNUMUNUOVOWAVAVNCD
      UAUPUQUOURUSUQ $.
      $( [22-Nov-1994] $)

    $( A way to express restricted uniqueness. $)
    reu6 $p |- ( E! x e. A ph <-> E. y e. A A. x e. A ( ph <-> x = y ) ) $=
      ( wreu cv wcel wa weu wceq wb wal wex wral wrex df-reu df-eu wi 19.28v
      wsbc eleq1 sbequ12 anbi12d eqeq1 bibi12d eqid tbt pm3.26 sylbir syl6bi
      a4imv bi1 expdimp bi2 pm3.27 syl6 adantr impbid ex a4s jca a5i imim2i
      imp3a adantl eleq1a imp com23 adantll jcai alimi impbii df-ral anbi2i
      3bitr4i exbii df-rex bitr4i 3bitri ) ABDEBFZDGZAHZBIWBVTCFZJZKZBLZCMZAWDK
      ZBDNZCDOZABDPWBBCQWGWCDGZWIHZCMWJWFWLCWKWAWHRZHZBLZWKWMBLZHWFWLWKWMBSWFWO
      WEWNBWFWKWMWEWKBCWDWEWKABWCTZHZWCWCJZKZWKWDWBWRWDWSWDWAWKAWQVTWCDUAABCUBU
      CVTWCWCUDUEWTWRWKWSWRWCUFUGWKWQUHUIUJUKWEWMBWEWAWHWEWAHAWDWEWAAWDWBWDULUM
      WEWDARZWAWEWDWBAWBWDUNWAAUOUPUQURUSUTVAVBWNWEBWNWBWDWMWBWDRWKWMWAAWDWHAWD
      RWAAWDULVCVDVEWNWDWBWNWDHWAAWNWDWAWKWDWARWMWCDVTVFUQVGWMWDWAARZWKWMWDXBWM
      WAWDAWHXAWAAWDUNVCVHVGVIVJUSURVKVLWIWPWKWHBDVMVNVOVPWICDVQVRVS $.
      $( [21-Oct-2006] $) $( [20-Oct-2006] $)

    $( A way to express restricted uniqueness. $)
    reu3 $p |- ( E! x e. A ph <->
               ( E. x e. A ph /\ E. y e. A A. x e. A ( ph -> x = y ) ) ) $=
      ( wreu wrex cv wceq wi wral wa reurex wb reu6 bi1 ralimi reximi sylbi
      jca wex rexex anim2i wcel weu wal ax-17 eu3 df-reu df-rex df-ral impexp
      albii bitr4i exbii anbi12i 3bitr4i sylibr impbii ) ABDEZABDFZABGZCGHZIZBD
      JZCDFZKZUSUTVEABDLUSAVBMZBDJZCDFVEABCDNVHVDCDVGVCBDAVBOPQRSVFUTVDCTZKZUSV
      EVIUTVDCDUAUBVADUCZAKZBUDVLBTZVLVBIZBUEZCTZKUSVJVLBCVLCUFUGABDUHUTVMVIVPA
      BDUIVDVOCVDVKVCIZBUEVOVCBDUJVNVQBVKAVBUKULUMUNUOUPUQUR $.
      $( [1-Nov-2006] $) $( [24-Oct-2006] $)
  $}

  ${
    $d x y z A $.  $d y z ph $.  $d x z ps $.
    rmo4.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Restricted "at most one" using implicit substitution. $)
    rmo4 $p |- ( E* x ( x e. A /\ ph ) <->
               A. x e. A A. y e. A ( ( ph /\ ps ) -> x = y ) ) $=
      ( cv wcel wa wceq wi wal wral wmo an4 ancom anbi1i bitri imbi1i impexp
      3bitri albii df-ral r19.21v 3bitr2i eleq1 anbi12d mo4 3bitr4i ) CGZEHZAIZ
      DGZEHZBIZIZUJUMJZKZDLZCLUKABIZUQKZDEMZKZCLULCNVBCEMUSVCCUSUNUKVAKZKZDLVDD
      EMVCURVEDURUNUKIZUTIZUQKVFVAKVEUPVGUQUPUKUNIZUTIVGUKAUNBOVHVFUTUKUNPQRSVF
      UTUQTUNUKVATUAUBVDDEUCUKVADEUDUEUBULUOCDUQUKUNABUJUMEUFFUGUHVBCEUCUI $.
      $( [24-Oct-2006] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu4 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
             A. x e. A A. y e. A ( ( ph /\ ps ) -> x = y ) ) ) $=
      ( wreu wrex cv wcel wa wmo wceq wi wral reu5 rmo4 anbi2i bitri ) ACEGACEH
      ZCIZEJAKCLZKTABKUADIMNDEOCEOZKACEPUBUCTABCDEFQRS $.
      $( [23-Nov-1994] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu7 $p |- ( E! x e. A ph <-> ( E. x e. A ph /\
             E. x e. A A. y e. A ( ps -> x = y ) ) ) $=
      ( vz wreu wrex cv wceq wi wral wa reu3 eqeq1 eqcom syl6bb imbi12d
      cbvralv rexbii imbi2d ralbidv cbvrexv bitri anbi2i ) ACEHACEIZACJZGJZKZLZ
      CEMZGEIZNUGBUHDJZKZLZDEMZCEIZNACGEOUMURUGUMBUIUNKZLZDEMZGEIURULVAGEUKUTCD
      EUOABUJUSFUOUJUNUIKUSUHUNUIPUNUIQRSTUAVAUQGCEUIUHKZUTUPDEVBUSUOBUIUHUNPUB
      UCUDUEUFUE $.
      $( [12-Nov-2006] $) $( [24-Oct-2006] $)

    $( Restricted uniqueness using implicit substitution. $)
    reu8 $p |- ( E! x e. A ph <-> E. x e. A ( ph /\
                A. y e. A ( ps -> x = y ) ) ) $=
      ( wreu cv wceq wb wral wrex wi wa cbvreuv reu6 wcel eqcom imbi2i ralbii
      a1i biimt wal df-ral bi2.04 albii visset eleq1 imbi12d bicomd eqcoms
      ceqsalv 3bitrri syl6bb anbi12d ancom syl5bb r19.26 syl6rbbr dfbi2
      rexbiia 3bitri ) ACEGBDEGBDHZCHZIZJZDEKZCELABVDVCIZMZDEKZNZCELABCDEFOBDCE
      PVGVKCEVDEQZBVEMZVEBMZNZDEKZVKVGVLVKVMDEKZVNDEKZNZVPVLVJANVSVKVLVJVQAVRVJ
      VQJVLVIVMDEVHVEBVDVCRSTUAVLAVLAMZVRVLAUBVRVCEQZVNMZDUCVEWABMZMZDUCVTVNDEU
      DWBWDDWAVEBUEUFWCVTDVDCUGWCVTJVDVCVHVTWCVHVLWAABVDVCEUHFUIUJUKULUMUNUOAVJ
      UPUQVMVNDEURUSVFVODEBVEUTTUQVAVB $.
      $( [25-Oct-2006] $) $( [24-Oct-2006] $)
  $}

  ${
    $d x y A $.
    $( A condition allowing swap of uniqueness and existential quantifiers. $)
    2reuswap $p |- ( A. x e. A E* y ( y e. A /\ ph ) ->
                   ( E! x e. A E. y e. A ph -> E! y e. A E. x e. A ph ) ) $=
      ( cv wcel wa wmo wral wal wrex wreu wi df-ral moanimv albii bitr4i wex
      weu 2euswap df-reu df-rex r19.42v an12 exbii 3bitr3i eubii bitri bitr3i
      3imtr4g sylbi ) CEDFZAGZCHZBDIZBEDFZUMGZCHZBJZACDKZBDLZABDKZCDLZMUOUPUNMZ
      BJUSUNBDNURVDBUPUMCOPQUSUQCRZBSZUQBRZCSZVAVCUQBCTVAUPUTGZBSVFUTBDUAVIVEBU
      PAGZCDKULVJGZCRVIVEVJCDUBUPACDUCVKUQCULUPAUDUEUFUGUHVCULVBGZCSVHVBCDUAVLV
      GCVLUMBDKVGULABDUCUMBDUBUIUGUHUJUK $.
      $( [16-Nov-2004] $) $( [14-Nov-2004] $)
  $}

  ${
    $d w y z A $.  $d x z B $.  $d w x y z C $.  $d w y z ph $.  $d x z ps $.
    reuind.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    reuind.2 $e |- ( x = y -> A = B ) $.
    $( Existential uniqueness via an indirect equality. $)
    reuind $p |- ( ( A. x A. y ( ( ( A e. C /\ ph ) /\ ( B e. C /\ ps ) )
         -> A = B ) /\ E. x ( A e. C /\ ph ) )
                 -> E! z e. C A. x ( ( A e. C /\ ph ) -> z = A ) ) $=
      ( vw wcel wa cv wceq wi wal wreu wrex wral wex eqeq1 imbi2d albidv reu4
      wb eqeq2 imim2i bi2 an23 ancom anbi1i 3bitri imbi1i impexp 3bitr3i sylib
      syl 2alimi 19.23v an12 eleq1 adantr pm5.32ri bitr4i exbii 19.42v bitri
      albii 19.21v exp3a reximdvai eleq1d anbi12d cbvexv risset rexcom4
      r19.41v bitr2i syl5ib imp biimpi com12 19.26 prth pm4.24 eqtr3 imim12i
      alimi sylbir syl5 a1d r19.21aivv adantl sylanbrc ) FHLZAMZENZFOZPZCQZEHRX
      AEHSZXAWQKNZFOZPZCQZMZWRXCOZPZKHTEHTZWQGHLZBMZMZFGOZPZDQCQZWQCUAZMXAXFEKH
      XHWTXECXHWSXDWQWRXCFUBUCUDUEXPXQXBXPWRGOZBMZDUAZEHSZXBXQXPXTXAEHXPWRHLZXT
      XAXPXRXLMZWTPZDQZCQZYBXTMZXAPZXOYDCDXOXMWSXRUFZPZYDXNYIXMFGWRUGUHYJXMXRWS
      PZPZYDYIYKXMWSXRUIUHXMXRMZWSPYCWQMZWSPYLYDYMYNWSYMWQXRMZXLMXRWQMZXLMYNWQX
      LXRUJYOYPXLWQXRUKULXRWQXLUJUMUNXMXRWSUOYCWQWSUOUPUQURUSYFYGWTPZCQYHYEYQCY
      EYCDUAZWTPYQYCWTDUTYRYGWTYRYBXSMZDUAYGYCYSDYCXKXSMYSXRXKBVAXSYBXKXRYBXKUF
      BWRGHVBVCVDVEVFYBXSDVGVHUNVHVIYGWTCVJVHUQVKVLXQXLDUAXREHSZBMZDUAZYAWQXLCD
      CNDNOZWPXKABUUCFGHJVMIVNVOXLUUADXKYTBEGHVPULVFYAXSEHSZDUAUUBXSEDHVQUUDUUA
      DXRBEHVRVFVSUMVTWAXQXJXPXQXIEKHHXQXIYBXCHLMXQWQXHPZCQZXHXGUUFXQXHUUFXQXHP
      WQXHCUTWBWCXGWTXEMZCQUUFWTXECWDUUGUUECUUGWQWQMZWSXDMZPUUEWQWSWQXDWEWQUUHU
      UIXHWQUUHWQWFWBWRXCFWGWHURWIWJWKWLWMWNWO $.
      $( [16-Oct-2010] $)
  $}

