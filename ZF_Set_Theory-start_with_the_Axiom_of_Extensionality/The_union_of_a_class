$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                       The union of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class union symbol. $)
  $c U. $. $( Big cup $)

  $( Extend class notation to include the union of a class (read:  'union
     ` A ` ') $)
  cuni $a class U. A $.

  ${
    $d x y A $.
    $( Define the union of a class i.e. the collection of all members of the
       members of the class.  Definition 5.5 of [TakeutiZaring] p. 16. $)
    df-uni $a |- U. A = { x | E. y ( x e. y /\ y e. A ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class union. $)
    dfuni2 $p |- U. A = { x | E. y e. A x e. y } $=
      ( cuni cv wcel wa wex cab wrex df-uni exancom df-rex bitr4i abbii eqtri
      ) CDAEBEZFZQCFZGBHZAIRBCJZAIABCKTUAATSRGBHUARSBLRBCMNOP $.
      $( [28-Jun-1998] $)
  $}

  ${
    $d x A y $.  $d x B y $.
    $( Membership in class union. $)
    eluni $p |- ( A e. U. B <-> E. x ( A e. x /\ x e. B ) ) $=
      ( vy cuni wcel cvv cv wa wex elisset adantr 19.23aiv wceq eleq1 anbi1d
      exbidv df-uni elab2g pm5.21nii ) BCEZFBGFZBAHZFZUCCFZIZAJZBUAKUFUBAUDUBUE
      BUCKLMDHZUCFZUEIZAJUGDBUAGUHBNZUJUFAUKUIUDUEUHBUCOPQDACRST $.
      $( [22-May-1994] $)

    $( Membership in class union.  Restricted quantifier version. $)
    eluni2 $p |- ( A e. U. B <-> E. x e. B A e. x ) $=
      ( cv wcel wa wex cuni wrex exancom eluni df-rex 3bitr4i ) BADZEZNCEZFAGPO
      FAGBCHEOACIOPAJABCKOACLM $.
      $( [31-Aug-1999] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Membership in class union. $)
    elunii $p |- ( ( A e. B /\ B e. C ) -> A e. U. C ) $=
      ( vx wcel wa cv wex cuni wceq eleq2 eleq1 anbi12d cla4egv anabsi7 eluni
      sylibr ) ABEZBCEZFZADGZEZUACEZFZDHZACIERSUEUDTDBCUABJUBRUCSUABAKUABCLMNOD
      ACPQ $.
      $( [24-Mar-1995] $)
  $}

  ${
    $d y z A $.  $d x y z $.
    hbuni.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for union.  (The proof was shortened
       by Andrew Salmon, 27-Aug-2011.) $)
    hbuni $p |- ( y e. U. A -> A. x y e. U. A ) $=
      ( vz cv wcel wa wex wal cuni ax-17 hblem hban hbex eluni albii 3imtr4i )
      BFZEFZGZTCGZHZEIZUDAJSCKGZUEAJUCAEUAUBAUAALABECDMNOESCPZUEUDAUFQR $.
      $( [27-Aug-2011] $) $( [30-Dec-1996] $)

    $( Bound-variable hypothesis builder for union. $)
    hbuniOLD $p |- ( y e. U. A -> A. x y e. U. A ) $=
      ( vz cv wcel wa wex wal cuni ax-17 hbel hban hbex eluni albii 3imtr4i )
      BFZEFZGZTCGZHZEIZUDAJSCKGZUEAJUCAEUAUBAUAALZABBTCUFDMNOESCPZUEUDAUGQR $.
      $( [30-Dec-1996] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for class union.  Exercise 15 of [TakeutiZaring]
       p. 18.  (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    unieq $p |- ( A = B -> U. A = U. B ) $=
      ( vy vx wceq cv wcel wrex cab cuni rexeq abbidv dfuni2 3eqtr4g ) ABEZCFDF
      GZDAHZCIPDBHZCIAJBJOQRCPDABKLCDAMCDBMN $.
      $( [29-Jun-2011] $) $( [10-Aug-1993] $)

    $( Equality theorem for class union.  Exercise 15 of [TakeutiZaring]
       p. 18. $)
    unieqOLD $p |- ( A = B -> U. A = U. B ) $=
      ( vx vy wceq cv wcel wa wex cab cuni eleq2 anbi2d exbidv abbidv df-uni
      3eqtr4g ) ABEZCFDFZGZSAGZHZDIZCJTSBGZHZDIZCJAKBKRUCUFCRUBUEDRUAUDTABSLMNO
      CDAPCDBPQ $.
      $( [10-Aug-1993] $)
  $}

  ${
    unieqi.1 $e |- A = B $.
    $( Inference of equality of two class unions. $)
    unieqi $p |- U. A = U. B $=
      ( wceq cuni unieq ax-mp ) ABDAEBEDCABFG $.
      $( [30-Aug-1993] $)
  $}

  ${
    unieqd.1 $e |- ( ph -> A = B ) $.
    $( Deduction of equality of two class unions. $)
    unieqd $p |- ( ph -> U. A = U. B ) $=
      ( wceq cuni unieq syl ) ABCEBFCFEDBCGH $.
      $( [21-Apr-1995] $)
  $}

  ${
    $d x A y $.  $d ph y $.
    $( Membership in union of a class abstraction. $)
    eluniab $p |- ( A e. U. { x | ph } <-> E. x ( A e. x /\ ph ) ) $=
      ( vy cab cuni wcel cv wa wex eluni ax-17 hbab1 hban wceq eleq2 eleq1
      anbi12d abid anbi2i syl6bb cbvex bitri ) CABEZFGCDHZGZUEUDGZIZDJCBHZGZAIZ
      BJDCUDKUHUKDBUFUGBUFBLABDMNUKDLUEUIOZUHUJUIUDGZIUKULUFUJUGUMUEUICPUEUIUDQ
      RUMAUJABSTUAUBUC $.
      $( [11-Aug-1994] $)

    $( Membership in union of a class abstraction. $)
    elunirab $p |- ( A e. U. { x e. B | ph } <->
                   E. x e. B ( A e. x /\ ph ) ) $=
      ( cv wcel wa cab cuni wex crab wrex eluniab df-rab unieqi eleq2i df-rex
      an12 exbii bitri 3bitr4i ) CBEZDFZAGZBHZIZFCUBFZUDGZBJZCABDKZIZFUGAGZBDLZ
      UDBCMUKUFCUJUEABDNOPUMUCULGZBJUIULBDQUNUHBUCUGARSTUA $.
      $( [4-Oct-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    unipr.1 $e |- A e. _V $.
    unipr.2 $e |- B e. _V $.
    $( The union of a pair is the union of its members.  Proposition 5.7 of
       [TakeutiZaring] p. 16. $)
    unipr $p |- U. { A , B } = ( A u. B ) $=
      ( vx vy cv wcel wo cab cpr wa wex cun cuni wceq 19.43 visset elpr anbi2i
      andi bitri exbii clel3 exancom orbi12i 3bitr4ri abbii df-un df-uni
      3eqtr4ri ) EGZAHZULBHZIZEJULFGZHZUPABKZHZLZFMZEJABNUROUOVAEUQUPAPZLZUQUPB
      PZLZIZFMVCFMZVEFMZIVAUOVCVEFQUTVFFUTUQVBVDIZLVFUSVIUQUPABFRSTUQVBVDUAUBUC
      UMVGUNVHUMVBUQLFMVGFULACUDVBUQFUEUBUNVDUQLFMVHFULBDUDVDUQFUEUBUFUGUHEABUI
      EFURUJUK $.
      $( [23-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The union of a pair is the union of its members.  Proposition 5.7 of
       [TakeutiZaring] p. 16. $)
    uniprg $p |- ( ( A e. C /\ B e. D ) -> U. { A , B } = ( A u. B ) ) $=
      ( vx vy cv cpr cuni cun wceq preq1 unieqd uneq1 eqeq12d preq2 uneq2
      visset unipr vtocl2g ) EGZFGZHZIZUAUBJZKAUBHZIZAUBJZKABHZIZABJZKEFABCDUAA
      KZUDUGUEUHULUCUFUAAUBLMUAAUBNOUBBKZUGUJUHUKUMUFUIUBBAPMUBBAQOUAUBERFRST
      $.
      $( [25-Aug-2006] $)
  $}

  ${
    unisn.1 $e |- A e. _V $.
    $( A set equals the union of its singleton.  Theorem 8.2 of [Quine]
       p. 53. $)
    unisn $p |- U. { A } = A $=
      ( csn cuni cpr cun dfsn2 unieqi unipr unidm 3eqtri ) ACZDAAEZDAAFALMAGHAA
      BBIAJK $.
      $( [30-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( A set equals the union of its singleton.  Theorem 8.2 of [Quine]
       p. 53. $)
    unisng $p |- ( A e. B -> U. { A } = A ) $=
      ( vx cv csn cuni wceq sneq unieqd id eqeq12d visset unisn vtoclg ) CDZEZF
      ZOGAEZFZAGCABOAGZQSOATPROAHITJKOCLMN $.
      $( [13-Aug-2002] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbeqel.1 $e |- A e. _V $.
    hbeqel.2 $e |- ( y = A -> A. x y = A ) $.
    $( If ` x ` is effectively bound in ` y = A ` , then it is effectively
       bound in ` y e. A ` .  This is the other direction of ~ hbeleq , showing
       the two conditions are equivalent when ` A ` is a set. $)
    hbeqel $p |- ( y e. A -> A. x y e. A ) $=
      ( csn cuni unisn eqcomi cv wceq wal wcel elsn albii 3imtr4i hbuni hbxfr
      ) ABCCFZGZTCCDHIABSBJZCKZUBALUASMZUCALEBCNZUCUBAUDOPQR $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The class union of the union of two classes.  Theorem 8.3 of [Quine]
       p. 53. $)
    uniun $p |- U. ( A u. B ) = ( U. A u. U. B ) $=
      ( vx vy cun cuni cv wcel wa wex wo 19.43 elun anbi2i andi bitri exbii
      eluni orbi12i 3bitr4i eqriv ) CABEZFZAFZBFZEZCGZDGZHZUHUBHZIZDJZUGUDHZUGU
      EHZKZUGUCHUGUFHUIUHAHZIZUIUHBHZIZKZDJUQDJZUSDJZKULUOUQUSDLUKUTDUKUIUPURKZ
      IUTUJVCUIUHABMNUIUPUROPQUMVAUNVBDUGARDUGBRSTDUGUBRUGUDUEMTUA $.
      $( [20-Aug-1993] $)

    $( The class union of the intersection of two classes.  Exercise 4.12(n) of
       [Mendelson] p. 235.  See ~ uninqs for a condition where equality holds.
       (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    uniin $p |- U. ( A i^i B ) C_ ( U. A i^i U. B ) $=
      ( vx vy cin cuni cv wcel wa wex 19.40 elin anbi2i anandi bitri exbii
      eluni anbi12i 3imtr4i ssriv ) CABEZFZAFZBFZEZCGZDGZHZUGUAHZIZDJZUFUCHZUFU
      DHZIZUFUBHUFUEHUHUGAHZIZUHUGBHZIZIZDJUPDJZURDJZIUKUNUPURDKUJUSDUJUHUOUQIZ
      IUSUIVBUHUGABLMUHUOUQNOPULUTUMVADUFAQDUFBQRSDUFUAQUFUCUDLST $.
      $( [29-Jun-2011] $) $( [4-Dec-2003] $)

    $( The class union of the intersection of two classes.  Exercise 4.12(n) of
       [Mendelson] p. 235.  See ~ uninqs for a condition where equality
       holds. $)
    uniinOLD $p |- U. ( A i^i B ) C_ ( U. A i^i U. B ) $=
      ( vx vy cin cuni cv wcel wa wex 19.40 eluni elin anbi2i anandi bitri
      exbii anbi12i 3imtr4i ssriv ) CABEZFZAFZBFZEZCGZDGZHZUGAHZIZUHUGBHZIZIZDJ
      ZUJDJZULDJZIZUFUBHZUFUEHZUJULDKURUHUGUAHZIZDJUNDUFUALVAUMDVAUHUIUKIZIUMUT
      VBUHUGABMNUHUIUKOPQPUSUFUCHZUFUDHZIUQUFUCUDMVCUOVDUPDUFALDUFBLRPST $.
      $( [4-Dec-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Subclass relationship for class union.  Theorem 61 of [Suppes] p. 39.
       (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    uniss $p |- ( A C_ B -> U. A C_ U. B ) $=
      ( vx vy wss cuni cv wcel wa wex ssel anim2d eximdv eluni 3imtr4g ssrdv )
      ABEZCAFZBFZQCGZDGZHZUAAHZIZDJUBUABHZIZDJTRHTSHQUDUFDQUCUEUBABUAKLMDTANDTB
      NOP $.
      $( [29-Jun-2011] $) $( [22-Mar-1998] $)

    $( Subclass relationship for class union.  Theorem 61 of [Suppes] p. 39. $)
    unissOLD $p |- ( A C_ B -> U. A C_ U. B ) $=
      ( vx vy wss cv wcel wa wex cab cuni ssel anim2d eximdv ss2abdv df-uni
      3sstr4g ) ABEZCFDFZGZSAGZHZDIZCJTSBGZHZDIZCJAKBKRUCUFCRUBUEDRUAUDTABSLMNO
      CDAPCDBPQ $.
      $( [22-Mar-1998] $)

    $( Subclass relationship for class union.  (The proof was shortened by
       Andrew Salmon, 29-Jun-2011.) $)
    ssuni $p |- ( ( A C_ B /\ B e. C ) -> A C_ U. C ) $=
      ( vy vx wcel wss cuni cv wi wal wceq eleq2 imbi1d wa wex 19.8a eluni
      sylibr expcom vtoclga imim2d alimdv dfss2 3imtr4g impcom ) BCFZABGZACHZGZ
      UGDIZAFZUKBFZJZDKULUKUIFZJZDKUHUJUGUNUPDUGUMUOULUKEIZFZUOJUMUOJEBCUQBLURU
      MUOUQBUKMNURUQCFZUOURUSOZUTEPUOUTEQEUKCRSTUAUBUCDABUDDAUIUDUEUF $.
      $( [29-Jun-2011] $) $( [24-May-1994] $)

    $( Subclass relationship for class union. $)
    ssuniOLD $p |- ( ( A C_ B /\ B e. C ) -> A C_ U. C ) $=
      ( vx vy wcel wss cuni cv wi wceq sseq2 imbi1d wal wa wex 19.8a expcom
      eluni syl6ibr imim2d alimdv dfss2 3imtr4g vtoclga impcom ) BCFABGZACHZGZA
      DIZGZUIJUGUIJDBCUJBKUKUGUIUJBALMUJCFZEIZAFZUMUJFZJZENUNUMUHFZJZENUKUIULUP
      UREULUOUQUNULUOUOULOZDPZUQUOULUTUSDQRDUMCSTUAUBEAUJUCEAUHUCUDUEUF $.
      $( [24-May-1994] $)
  $}

  ${
    $d x y A $.
    $( The union of a set is empty iff the set is included in the singleton of
       the empty set. $)
    uni0b $p |- ( U. A = (/) <-> A C_ { (/) } ) $=
      ( vx vy cv c0 csn wcel wral wceq wss cuni elsn ralbii dfss3 wn wex wrex
      neq0 rexcom4 rexbii eluni2 exbii 3bitr4ri rexnal 3bitri con4bii ) BDZEFZG
      ZBAHUGEIZBAHZAUHJAKZEIZUIUJBABELMBAUHNUMUKUMOCDZULGZCPZUJOZBAQZUKOCULRUNU
      GGZCPZBAQUSBAQZCPURUPUSBCASUQUTBACUGRTUOVACBUNAUAUBUCUJBAUDUEUFUC $.
      $( [12-Sep-2004] $)

    $( The union of a set is empty iff all of its members are empty. $)
    uni0c $p |- ( U. A = (/) <-> A. x e. A x = (/) ) $=
      ( cuni c0 wceq csn wss cv wcel wral uni0b dfss3 elsn ralbii 3bitri ) BCDE
      BDFZGAHZPIZABJQDEZABJBKABPLRSABADMNO $.
      $( [16-Aug-2006] $)
  $}

  $( The union of the empty set is the empty set.  Theorem 8.7 of [Quine]
     p. 54.  (Reproved without relying on ~ ax-nul by Eric Schmidt,
     4-Apr-2007.) $)
  uni0 $p |- U. (/) = (/) $=
    ( c0 cuni wceq csn wss 0ss uni0b mpbir ) ABACAADZEIFAGH $.
    $( [4-Apr-2007] $) $( [16-Sep-1993] $)

  $( An element of a class is a subclass of its union.  Theorem 8.6 of [Quine]
     p. 54.  Also the basis for Proposition 7.20 of [TakeutiZaring] p. 40. $)
  elssuni $p |- ( A e. B -> A C_ U. B ) $=
    ( wss wcel cuni ssid ssuni mpan ) AACABDABECAFAABGH $.
    $( [6-Jun-1994] $)

  $( Condition turning a subclass relationship for union into an equality. $)
  unissel $p |- ( ( U. A C_ B /\ B e. A ) -> U. A = B ) $=
    ( cuni wss wcel wa simpl elssuni adantl eqssd ) ACZBDZBAEZFKBLMGMBKDLBAHIJ
    $.
    $( [18-Jul-2006] $)

  ${
    $d x y A $.  $d x y B $.
    $( Relationship involving membership, subset, and union.  Exercise 5 of
       [Enderton] p. 26 and its converse. $)
    unissb $p |- ( U. A C_ B <-> A. x e. A x C_ B ) $=
      ( vy cuni wss cv wcel wi wal wral wa dfss2 wex eluni imbi1i 19.23v
      bitr4i albii alcom 19.21v impexp bi2.04 bitri imbi2i 3bitr4i 3bitri
      df-ral ) BEZCFZAGZBHZUKCFZIZAJZUMABKUJDGZUIHZUPCHZIZDJUPUKHZULLZURIZAJZDJ
      ZUODUICMUSVCDUSVAANZURIVCUQVEURAUPBOPVAURAQRSVDVBDJZAJUOVBDATVFUNAULUTURI
      ZIZDJULVGDJZIVFUNULVGDUAVBVHDVBUTULURIIVHUTULURUBUTULURUCUDSUMVIULDUKCMUE
      UFSUDUGUMABUHR $.
      $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.  $d x y B $.
    $( A subclass condition on the members of two classes that implies a
       subclass relation on their unions.  Proposition 8.6 of [TakeutiZaring]
       p. 59.  See ~ iunss2 for a generalization to indexed unions. $)
    uniss2 $p |- ( A. x e. A E. y e. B x C_ y -> U. A C_ U. B ) $=
      ( cv wss wrex wral cuni wcel ssuni expcom r19.23aiv ralimi unissb sylibr
      ) AEZBEZFZBDGZACHQDIZFZACHCIUAFTUBACSUBBDSRDJUBQRDKLMNACUAOP $.
      $( [22-Mar-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( If the difference ` A \ B ` contains the largest members of ` A ` , then
       the union of the difference is the union of ` A ` . $)
    unidif $p |- ( A. x e. A E. y e. ( A \ B ) x C_ y ->
               U. ( A \ B ) = U. A ) $=
      ( cv wss cdif wrex wral cuni wa wceq uniss2 difss uniss ax-mp jctil eqss
      sylibr ) AEBEFBCDGZHACIZTJZCJZFZUCUBFZKUBUCLUAUEUDABCTMTCFUDCDNTCOPQUBUCR
      S $.
      $( [22-Mar-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Relationship implying union. $)
    ssunieq $p |- ( ( A e. B /\ A. x e. B x C_ A ) -> A = U. B ) $=
      ( wcel cv wss wral wa cuni wceq elssuni unissb biimpri anim12i eqss
      sylibr ) BCDZAEBFACGZHBCIZFZSBFZHBSJQTRUABCKUARACBLMNBSOP $.
      $( [10-Nov-1999] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Any member of a class is the largest of those members that it
       includes. $)
    unimax $p |- ( A e. B -> U. { x e. B | x C_ A } = A ) $=
      ( vy cv wss crab wcel wral cuni wceq wa ssunieq eqcomd ssid sseq1 elrab3
      mpbiri elrab simprbi rgen sylancl ) BAEZBFZACGZHZDEZBFZDUEIZUEJZBKBCHZUFU
      ILBUJDBUEMNUKUFBBFZBOUDULABCUCBBPQRUHDUEUGUEHUGCHUHUDUHAUGCUCUGBPSTUAUB
      $.
      $( [9-Oct-2006] $) $( [13-Aug-2002] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Closure law for "the unique element in ` A ` such that ` ph ` ." $)
    reucl $p |- ( E! x e. A ph -> U. { x e. A | ph } e. A ) $=
      ( vy cv wcel wa weu cab cuni wreu crab csn wceq wex euabsn hbab1 hbuni
      ax-17 hbel unieq visset unisn syl6req snid eleq2 mpbiri abid sylib
      simpld eqeltrrd 19.23ai sylbi df-reu df-rab unieqi eleq1i 3imtr4i ) BEZCF
      ZAGZBHZVABIZJZCFZABCKABCLZJZCFVBVCUSMZNZBOVEVABPVIVEBBDDVDCBDVCVABDQRDECF
      BSTVIUSVDCVIVDVHJUSVCVHUAUSBUBZUCUDVIUTAVIUSVCFZVAVIVKUSVHFUSVJUEVCVHUSUF
      UGVABUHUIUJUKULUMABCUNVGVDCVFVCABCUOUPUQUR $.
      $( [22-Nov-1994] $)
  $}

