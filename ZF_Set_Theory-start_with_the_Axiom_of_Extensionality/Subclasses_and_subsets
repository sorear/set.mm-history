$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subclasses and subsets
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( Alternate definition of the subclass relationship between two classes.
       Definition 5.9 of [TakeutiZaring] p. 17. $)
    dfss2 $p |- ( A (_ B <-> A. x ( x e. A -> x e. B ) ) $=
      ( wss cv wcel wa wb wal wi cin wceq cab dfss df-in eqeq2i abeq2 3bitri
      pm4.71 albii bitr4i ) BCDZAEZBFZUDUCCFZGZHZAIZUDUEJZAIUBBBCKZLBUFAMZLUHBC
      NUJUKBABCOPUFABQRUIUGAUDUESTUA $.
      $( [8-Jan-2002] $)

    $( Alternate definition of subclass relationship. $)
    dfss3 $p |- ( A (_ B <-> A. x e. A x e. B ) $=
      ( wss cv wcel wi wal wral dfss2 df-ral bitr4i ) BCDAEZBFMCFZGAHNABIABCJNA
      BKL $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    dfss2f.1 $e |- ( y e. A -> A. x y e. A ) $.
    dfss2f.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Equivalence for subclass relation, using bound-variable hypotheses
       instead of distinct variable conditions. $)
    dfss2f $p |- ( A (_ B <-> A. x ( x e. A -> x e. B ) ) $=
      ( wss cv wcel wi wal dfss2 ax-17 hbim wceq eleq1 imbi12d cbval bitr4i )
      CDGBHZCIZTDIZJZBKAHZCIZUDDIZJZAKBCDLUGUCABUGBMUAUBAEFNUDTOUEUAUFUBUDTCPUD
      TDPQRS $.
      $( [3-Jul-1994] $)

    $( Equivalence for subclass relation, using bound-variable hypotheses
       instead of distinct variable conditions. $)
    dfss3f $p |- ( A (_ B <-> A. x e. A x e. B ) $=
      ( wss cv wcel wi wal wral dfss2f df-ral bitr4i ) CDGAHZCIPDIZJAKQACLABCDE
      FMQACNO $.
      $( [21-Mar-2004] $) $( [20-Mar-2004] $)

    $( If ` x ` is not free in ` A ` and ` B ` , it is not free in
       ` A (_ B ` . $)
    hbss $p |- ( A (_ B -> A. x A (_ B ) $=
      ( cv wcel wi wal wss hba1 dfss2f albii 3imtr4i ) AGZCHPDHIZAJZRAJCDKZSAJQ
      ALABCDEFMZSRATNO $.
      $( [27-Dec-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Membership relationships follow from a subclass relationship. $)
    ssel $p |- ( A (_ B -> ( C e. A -> C e. B ) ) $=
      ( vx wss cv wceq wcel wa wex wi wal dfss2 biimpi 19.21bi anim2d 19.22dv
      df-clel 3imtr4g ) ABEZDFZCGZUAAHZIZDJUBUABHZIZDJCAHCBHTUDUFDTUCUEUBTUCUEK
      ZDTUGDLDABMNOPQDCARDCBRS $.
      $( [5-Aug-1993] $)
  $}

  $( Membership relationships follow from a subclass relationship. $)
  ssel2 $p |- ( ( A (_ B /\ C e. A ) -> C e. B ) $=
    ( wss wcel ssel imp ) ABDCAECBEABCFG $.
    $( [8-Jun-2004] $) $( [7-Jun-2004] $)

  ${
    sseli.1 $e |- A (_ B $.
    $( Membership inference from subclass relationship. $)
    sseli $p |- ( C e. A -> C e. B ) $=
      ( wss wcel wi ssel ax-mp ) ABECAFCBFGDABCHI $.
      $( [5-Aug-1993] $)
    ${
      sselii.2 $e |- C e. A $.
      $( Membership inference from subclass relationship. $)
      sselii $p |- C e. B $=
        ( wcel sseli ax-mp ) CAFCBFEABCDGH $.
        $( [31-May-1999] $)
    $}
  $}

  ${
    sseld.1 $e |- ( ph -> A (_ B ) $.
    $( Membership deduction from subclass relationship. $)
    sseld $p |- ( ph -> ( C e. A -> C e. B ) ) $=
      ( wss wcel wi ssel syl ) ABCFDBGDCGHEBCDIJ $.
      $( [15-Nov-1995] $)
    ${
      sseldd.2 $e |- ( ph -> C e. A ) $.
      $( Membership inference from subclass relationship. $)
      sseldd $p |- ( ph -> C e. B ) $=
        ( wcel sseld mpd ) ADBGDCGFABCDEHI $.
        $( [15-Dec-2004] $) $( [14-Dec-2004] $)
    $}
  $}

  ${
    $d x A $.  $d x B $.
    ssriv.1 $e |- ( x e. A -> x e. B ) $.
    $( Inference rule based on subclass definition. $)
    ssriv $p |- A (_ B $=
      ( wss cv wcel wi dfss2 mpgbir ) BCEAFZBGKCGHAABCIDJ $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    ssrdv.1 $e |- ( ph -> ( x e. A -> x e. B ) ) $.
    $( Deduction rule based on subclass definition. $)
    ssrdv $p |- ( ph -> A (_ B ) $=
      ( cv wcel wi wal wss 19.21aiv dfss2 sylibr ) ABFZCGNDGHZBICDJAOBEKBCDLM
      $.
      $( [15-Nov-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Transitivity of subclasses.  Exercise 5 of [TakeutiZaring] p. 17. $)
    sstr2 $p |- ( A (_ B -> ( B (_ C -> A (_ C ) ) $=
      ( vx wss cv wcel wi wal dfss2 imim1 19.20ii 3imtr4g sylbi ) ABEDFZAGZOBGZ
      HZDIZBCEZACEZHDABJSQOCGZHZDIPUBHZDITUARUCUDDPQUBKLDBCJDACJMN $.
      $( [5-Aug-1993] $)
  $}

  $( Transitivity of subclasses.  Theorem 6 of [Suppes] p. 23. $)
  sstr $p |- ( ( A (_ B /\ B (_ C ) -> A (_ C ) $=
    ( wss sstr2 imp ) ABDBCDACDABCEF $.
    $( [5-Sep-2003] $)

  ${
    sstri.1 $e |- A (_ B $.
    sstri.2 $e |- B (_ C $.
    $( Subclass transitivity inference. $)
    sstri $p |- A (_ C $=
      ( wss sstr2 mp2 ) ABFBCFACFDEABCGH $.
      $( [5-May-2000] $)
  $}

  ${
    sstrd.1 $e |- ( ph -> A (_ B ) $.
    sstrd.2 $e |- ( ph -> B (_ C ) $.
    $( Subclass transitivity deduction. $)
    sstrd $p |- ( ph -> A (_ C ) $=
      ( wss sstr sylanc ) BCGCDGBDGABCDHEFI $.
      $( [4-Jun-2004] $) $( [2-Jun-2004] $)
  $}

  ${
    sylan9ss.1 $e |- ( ph -> A (_ B ) $.
    sylan9ss.2 $e |- ( ps -> B (_ C ) $.
    $( A subclass transitivity deduction. $)
    sylan9ss $p |- ( ( ph /\ ps ) -> A (_ C ) $=
      ( wa wss adantr adantl sstrd ) ABHCDEACDIBFJBDEIAGKL $.
      $( [28-Sep-2004] $) $( [27-Sep-2004] $)
  $}

  ${
    sylan9ssr.1 $e |- ( ph -> A (_ B ) $.
    sylan9ssr.2 $e |- ( ps -> B (_ C ) $.
    $( A subclass transitivity deduction. $)
    sylan9ssr $p |- ( ( ps /\ ph ) -> A (_ C ) $=
      ( wss sylan9ss ancoms ) ABCEHABCDEFGIJ $.
      $( [28-Oct-2004] $) $( [27-Sep-2004] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The subclass relationship is antisymmetric.  Compare Theorem 4 of
       [Suppes] p. 22. $)
    eqss $p |- ( A = B <-> ( A (_ B /\ B (_ A ) ) $=
      ( vx cv wcel wb wal wi wa wceq wss albi dfcleq dfss2 anbi12i 3bitr4i ) CD
      ZAEZQBEZFCGRSHCGZSRHCGZIABJABKZBAKZIRSCLCABMUBTUCUACABNCBANOP $.
      $( [5-Aug-1993] $)
  $}

  ${
    eqssi.1 $e |- A (_ B $.
    eqssi.2 $e |- B (_ A $.
    $( Infer equality from two subclass relationships.  Compare Theorem 4 of
       [Suppes] p. 22. $)
    eqssi $p |- A = B $=
      ( wceq wss eqss mpbir2an ) ABEABFBAFABGCDH $.
      $( [9-Sep-1993] $)
  $}

  ${
    eqssd.1 $e |- ( ph -> A (_ B ) $.
    eqssd.2 $e |- ( ph -> B (_ A ) $.
    $( Equality deduction from two subclass relationships.  Compare Theorem 4
       of [Suppes] p. 22. $)
    eqssd $p |- ( ph -> A = B ) $=
      ( wss wa wceq jca eqss sylibr ) ABCFZCBFZGBCHALMDEIBCJK $.
      $( [28-Jun-2004] $) $( [27-Jun-2004] $)
  $}

  $( Any class is a subclass of itself.  Exercise 10 of [TakeutiZaring]
     p. 18. $)
  ssid $p |- A (_ A $=
    ( wss wceq wa eqid eqss mpbi pm3.26i ) AABZIAACIIDAEAAFGH $.
    $( [5-Aug-1993] $)

  ${
    $d A x $.
    $( Any class is a subclass of the universal class. $)
    ssv $p |- A (_ V $=
      ( vx cvv cv elisset ssriv ) BACBDAEF $.
      $( [31-Oct-1995] $)
  $}

  $( Equality theorem for subclasses. $)
  sseq1 $p |- ( A = B -> ( A (_ C <-> B (_ C ) ) $=
    ( wss wb wa wi wceq sstr2 anim12i eqss dfbi2 3imtr4i eqcoms ) ACDZBCDZEZBAB
    ADZABDZFOPGZPOGZFBAHQRTSUABACIABCIJBAKOPLMN $.
    $( [5-Aug-1993] $)

  $( Equality theorem for the subclass relationship. $)
  sseq2 $p |- ( A = B -> ( C (_ A <-> C (_ B ) ) $=
    ( wss wa wi wceq wb sstr2 com12 anim12i eqss dfbi2 3imtr4i ) ABDZBADZECADZC
    BDZFZRQFZEABGQRHOSPTQORCABIJRPQCBAIJKABLQRMN $.
    $( [25-Jun-1998] $)

  $( Equality theorem for the subclass relationship. $)
  sseq12 $p |- ( ( A = B /\ C = D ) -> ( A (_ C <-> B (_ D ) ) $=
    ( wceq wss sseq1 sseq2 sylan9bb ) ABEACFBCFCDEBDFABCGCDBHI $.
    $( [31-May-1999] $)

  ${
    sseq1i.1 $e |- A = B $.
    $( An equality inference for the subclass relationship. $)
    sseq1i $p |- ( A (_ C <-> B (_ C ) $=
      ( wceq wss wb sseq1 ax-mp ) ABEACFBCFGDABCHI $.
      $( [18-Aug-1993] $)

    $( An equality inference for the subclass relationship. $)
    sseq2i $p |- ( C (_ A <-> C (_ B ) $=
      ( wceq wss wb sseq2 ax-mp ) ABECAFCBFGDABCHI $.
      $( [30-Aug-1993] $)

    ${
      sseq12i.2 $e |- C = D $.
      $( An equality inference for the subclass relationship.  (The proof was
         shortened by Eric Schmidt, 26-Jan-2007.) $)
      sseq12i $p |- ( A (_ C <-> B (_ D ) $=
        ( wceq wss wb sseq12 mp2an ) ABGCDGACHBDHIEFABCDJK $.
        $( [27-Jan-2007] $) $( [31-May-1999] $)
    $}
  $}

  ${
    sseq1d.1 $e |- ( ph -> A = B ) $.
    $( An equality deduction for the subclass relationship. $)
    sseq1d $p |- ( ph -> ( A (_ C <-> B (_ C ) ) $=
      ( wceq wss wb sseq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $( [14-Aug-1994] $)

    $( An equality deduction for the subclass relationship. $)
    sseq2d $p |- ( ph -> ( C (_ A <-> C (_ B ) ) $=
      ( wceq wss wb sseq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [14-Aug-1994] $)

    ${
      sseq12d.2 $e |- ( ph -> C = D ) $.
      $( An equality deduction for the subclass relationship. $)
      sseq12d $p |- ( ph -> ( A (_ C <-> B (_ D ) ) $=
        ( wss sseq1d sseq2d bitrd ) ABDHCDHCEHABCDFIADECGJK $.
        $( [31-May-1999] $)
    $}
  $}

  ${
    eqsstr.1 $e |- A = B $.
    eqsstr.2 $e |- B (_ C $.
    $( Substitution of equality into a subclass relationship. $)
    eqsstri $p |- A (_ C $=
      ( wss sseq1i mpbir ) ACFBCFEABCDGH $.
      $( [16-Jul-1995] $)
  $}

  ${
    eqsstr3.1 $e |- B = A $.
    eqsstr3.2 $e |- B (_ C $.
    $( Substitution of equality into a subclass relationship. $)
    eqsstr3i $p |- A (_ C $=
      ( eqcomi eqsstri ) ABCBADFEG $.
      $( [19-Oct-1999] $)
  $}

  ${
    sseqtr.1 $e |- A (_ B $.
    sseqtr.2 $e |- B = C $.
    $( Substitution of equality into a subclass relationship. $)
    sseqtri $p |- A (_ C $=
      ( wss sseq2i mpbi ) ABFACFDBCAEGH $.
      $( [28-Jul-1995] $)
  $}

  ${
    sseqtr4.1 $e |- A (_ B $.
    sseqtr4.2 $e |- C = B $.
    $( Substitution of equality into a subclass relationship. $)
    sseqtr4i $p |- A (_ C $=
      ( eqcomi sseqtri ) ABCDCBEFG $.
      $( [4-Apr-1995] $)
  $}

  ${
    eqsstrd.1 $e |- ( ph -> A = B ) $.
    eqsstrd.2 $e |- ( ph -> B (_ C ) $.
    $( Substitution of equality into a subclass relationship. $)
    eqsstrd $p |- ( ph -> A (_ C ) $=
      ( wss sseq1d mpbird ) ABDGCDGFABCDEHI $.
      $( [27-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    eqsstr3d.1 $e |- ( ph -> B = A ) $.
    eqsstr3d.2 $e |- ( ph -> B (_ C ) $.
    $( Substitution of equality into a subclass relationship. $)
    eqsstr3d $p |- ( ph -> A (_ C ) $=
      ( eqcomd eqsstrd ) ABCDACBEGFH $.
      $( [27-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    sseqtrd.1 $e |- ( ph -> A (_ B ) $.
    sseqtrd.2 $e |- ( ph -> B = C ) $.
    $( Substitution of equality into a subclass relationship. $)
    sseqtrd $p |- ( ph -> A (_ C ) $=
      ( wss sseq2d mpbid ) ABCGBDGEACDBFHI $.
      $( [27-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    sseqtr4d.1 $e |- ( ph -> A (_ B ) $.
    sseqtr4d.2 $e |- ( ph -> C = B ) $.
    $( Substitution of equality into a subclass relationship. $)
    sseqtr4d $p |- ( ph -> A (_ C ) $=
      ( eqcomd sseqtrd ) ABCDEADCFGH $.
      $( [27-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    3sstr3.1 $e |- A (_ B $.
    3sstr3.2 $e |- A = C $.
    3sstr3.3 $e |- B = D $.
    $( Substitution of equality in both sides of a subclass relationship.
       (The proof was shortened by Eric Schmidt, 26-Jan-2007.) $)
    3sstr3i $p |- C (_ D $=
      ( wss sseq12i mpbi ) ABHCDHEACBDFGIJ $.
      $( [27-Jan-2007] $) $( [13-Jan-1996] $)
  $}

  ${
    3sstr4.1 $e |- A (_ B $.
    3sstr4.2 $e |- C = A $.
    3sstr4.3 $e |- D = B $.
    $( Substitution of equality in both sides of a subclass relationship.
       (The proof was shortened by Eric Schmidt, 26-Jan-2007.) $)
    3sstr4i $p |- C (_ D $=
      ( wss sseq12i mpbir ) CDHABHECADBFGIJ $.
      $( [27-Jan-2007] $) $( [13-Jan-1996] $)
  $}

  ${
    3sstr3g.1 $e |- ( ph -> A (_ B ) $.
    3sstr3g.2 $e |- A = C $.
    3sstr3g.3 $e |- B = D $.
    $( Substitution of equality into both sides of a subclass relationship. $)
    3sstr3g $p |- ( ph -> C (_ D ) $=
      ( wss sseq12i sylib ) ABCIDEIFBDCEGHJK $.
      $( [1-Oct-2000] $)
  $}

  ${
    3sstr4g.1 $e |- ( ph -> A (_ B ) $.
    3sstr4g.2 $e |- C = A $.
    3sstr4g.3 $e |- D = B $.
    $( Substitution of equality into both sides of a subclass relationship.
       (The proof was shortened by Eric Schmidt, 26-Jan-2007.) $)
    3sstr4g $p |- ( ph -> C (_ D ) $=
      ( wss sseq12i sylibr ) ABCIDEIFDBECGHJK $.
      $( [27-Jan-2007] $) $( [16-Aug-1994] $)
  $}

  ${
    3sstr3d.1 $e |- ( ph -> A (_ B ) $.
    3sstr3d.2 $e |- ( ph -> A = C ) $.
    3sstr3d.3 $e |- ( ph -> B = D ) $.
    $( Substitution of equality into both sides of a subclass relationship. $)
    3sstr3d $p |- ( ph -> C (_ D ) $=
      ( wss sseq12d mpbid ) ABCIDEIFABDCEGHJK $.
      $( [1-Oct-2000] $)
  $}

  ${
    3sstr4d.1 $e |- ( ph -> A (_ B ) $.
    3sstr4d.2 $e |- ( ph -> C = A ) $.
    3sstr4d.3 $e |- ( ph -> D = B ) $.
    $( Substitution of equality into both sides of a subclass relationship.
       (The proof was shortened by Eric Schmidt, 26-Jan-2007.) $)
    3sstr4d $p |- ( ph -> C (_ D ) $=
      ( wss sseq12d mpbird ) ADEIBCIFADBECGHJK $.
      $( [27-Jan-2007] $) $( [30-Nov-1995] $)
  $}

  ${
    syl5ss.1 $e |- ( ph -> A (_ B ) $.
    syl5ss.2 $e |- C = A $.
    $( A chained subclass and equality deduction. $)
    syl5ss $p |- ( ph -> C (_ B ) $=
      ( wss sseq1i sylibr ) ABCGDCGEDBCFHI $.
      $( [26-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    syl5ssr.1 $e |- ( ph -> A (_ B ) $.
    syl5ssr.2 $e |- A = C $.
    $( A chained subclass and equality deduction. $)
    syl5ssr $p |- ( ph -> C (_ B ) $=
      ( eqcomi syl5ss ) ABCDEBDFGH $.
      $( [26-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    syl6ss.1 $e |- ( ph -> A (_ B ) $.
    syl6ss.2 $e |- B = C $.
    $( A chained subclass and equality deduction. $)
    syl6ss $p |- ( ph -> A (_ C ) $=
      ( wss sseq2i sylib ) ABCGBDGECDBFHI $.
      $( [26-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  ${
    syl6ssr.1 $e |- ( ph -> A (_ B ) $.
    syl6ssr.2 $e |- C = B $.
    $( A chained subclass and equality deduction. $)
    syl6ssr $p |- ( ph -> A (_ C ) $=
      ( eqcomi syl6ss ) ABCDEDCFGH $.
      $( [26-Apr-2004] $) $( [25-Apr-2004] $)
  $}

  $( Equality implies the subclass relation. $)
  eqimss $p |- ( A = B -> A (_ B ) $=
    ( wceq wss ssid sseq2 mpbii ) ABCAADABDAEABAFG $.
    $( [5-Aug-1993] $)

  $( Equality implies the subclass relation. $)
  eqimss2 $p |- ( B = A -> A (_ B ) $=
    ( wss eqimss eqcoms ) ABCABABDE $.
    $( [23-Nov-2003] $) $( [23-Nov-2003] $)

  ${
    eqimssi.1 $e |- A = B $.
    $( Infer subclass relationship from equality. $)
    eqimssi $p |- A (_ B $=
      ( ssid sseqtri ) AABADCE $.
      $( [6-Jan-2007] $) $( [6-Jan-2007] $)

    $( Infer subclass relationship from equality. $)
    eqimss2i $p |- B (_ A $=
      ( ssid sseqtr4i ) BBABDCE $.
      $( [7-Jan-2007] $) $( [7-Jan-2007] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Negation of subclass relationship.  Exercise 13 of [TakeutiZaring]
       p. 18. $)
    nss $p |- ( -. A (_ B <-> E. x ( x e. A /\ -. x e. B ) ) $=
      ( cv wcel wi wn wex wal wa wss exnal annim exbii dfss2 notbii 3bitr4ri )
      ADZBEZRCEZFZGZAHUAAIZGSTGJZAHBCKZGUAALUDUBASTMNUEUCABCOPQ $.
      $( [25-Feb-1996] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Quantification restricted to a subclass. $)
    ssralv $p |- ( A (_ B -> ( A. x e. B ph -> A. x e. A ph ) ) $=
      ( wss cv wcel ssel imim1d r19.20dv2 ) CDEZAABDCKBFZCGLDGACDLHIJ $.
      $( [12-Mar-2006] $) $( [11-Mar-2006] $)

    $( Existential quantification restricted to a subclass. $)
    ssrexv $p |- ( A (_ B -> ( E. x e. A ph -> E. x e. B ph ) ) $=
      ( wss cv wcel ssel anim1d r19.22dv2 ) CDEZAABCDKBFZCGLDGACDLHIJ $.
      $( [13-Jan-2007] $) $( [11-Jan-2007] $)
  $}

  ${
    $d ph y $.  $d ps y $.  $d x y $.
    $( Class abstractions in a subclass relationship. $)
    ss2ab $p |- ( { x | ph } (_ { x | ps } <-> A. x ( ph -> ps ) ) $=
      ( vy cab wss cv wcel wi wal hbab1 dfss2f abid imbi12i albii bitri ) ACEZB
      CEZFCGZQHZSRHZIZCJABIZCJCDQRACDKBCDKLUBUCCTAUABACMBCMNOP $.
      $( [3-Jul-1994] $)
  $}

  ${
    $d x A $.
    $( Class abstraction in a subclass relationship. $)
    abss $p |- ( { x | ph } (_ A <-> A. x ( ph -> x e. A ) ) $=
      ( cab wss cv wcel wi wal abid2 sseq2i ss2ab bitr3i ) ABDZCENBFCGZBDZEAOHB
      IPCNBCJKAOBLM $.
      $( [17-Aug-2006] $) $( [16-Aug-2006] $)

    $( Subclass of a class abstraction. $)
    ssab $p |- ( A (_ { x | ph } <-> A. x ( x e. A -> ph ) ) $=
      ( cab wss cv wcel wi wal abid2 sseq1i ss2ab bitr3i ) CABDZEBFCGZBDZNEOAHB
      IPCNBCJKOABLM $.
      $( [6-Sep-2006] $) $( [16-Aug-2006] $)

    $( The relation for a subclass of a class abstraction is equivalent to
       restricted quantification. $)
    ssabral $p |- ( A (_ { x | ph } <-> A. x e. A ph ) $=
      ( cab wss cv wcel wi wal wral ssab df-ral bitr4i ) CABDEBFCGAHBIABCJABCKA
      BCLM $.
      $( [16-Sep-2006] $) $( [6-Sep-2006] $)
  $}

  ${
    ss2abi.1 $e |- ( ph -> ps ) $.
    $( Inference of abstraction subclass from implication. $)
    ss2abi $p |- { x | ph } (_ { x | ps } $=
      ( cab wss wi ss2ab mpgbir ) ACEBCEFABGCABCHDI $.
      $( [31-Mar-1995] $)
  $}

  ${
    $d x ph $.  $d x A $.
    abssdv.1 $e |- ( ph -> ( ps -> x e. A ) ) $.
    $( Deduction of abstraction subclass from implication. $)
    abssdv $p |- ( ph -> { x | ps } (_ A ) $=
      ( cv wcel wi wal cab wss 19.21aiv abss sylibr ) ABCFDGHZCIBCJDKAOCELBCDMN
      $.
      $( [25-Feb-2006] $) $( [20-Jan-2006] $)
  $}

  ${
    $d x A $.
    abssi.1 $e |- ( ph -> x e. A ) $.
    $( Inference of abstraction subclass from implication. $)
    abssi $p |- { x | ph } (_ A $=
      ( cab cv wcel ss2abi abid2 sseqtri ) ABEBFCGZBECAKBDHBCIJ $.
      $( [22-Jan-2006] $) $( [20-Jan-2006] $)
  $}

  $( Restricted abstraction classes in a subclass relationship. $)
  ss2rab $p |- ( { x e. A | ph } (_ { x e. A | ps } <->
               A. x e. A ( ph -> ps ) ) $=
    ( crab wss cv wcel wa cab wi wal wral df-rab sseq12i ss2ab df-ral imdistan
    albii bitr2i 3bitri ) ACDEZBCDEZFCGDHZAIZCJZUDBIZCJZFUEUGKZCLZABKZCDMZUBUFU
    CUHACDNBCDNOUEUGCPULUDUKKZCLUJUKCDQUMUICUDABRSTUA $.
    $( [30-May-1999] $)

  ${
    $d x B $.
    $( Restricted class abstraction in a subclass relationship. $)
    rabss $p |- ( { x e. A | ph } (_ B <-> A. x e. A ( ph -> x e. B ) ) $=
      ( crab wss cv wcel wa cab wi wal wral df-rab sseq1i abss impexp albii
      df-ral bitr4i 3bitri ) ABCEZDFBGZCHZAIZBJZDFUEUCDHZKZBLZAUGKZBCMZUBUFDABC
      NOUEBDPUIUDUJKZBLUKUHULBUDAUGQRUJBCSTUA $.
      $( [18-Sep-2006] $) $( [16-Aug-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Subclass of a restricted class abstraction. $)
    ssrab $p |- ( B (_ { x e. A | ph } <-> ( B (_ A /\ A. x e. B ph ) ) $=
      ( crab wss cv wcel wa cab wi wal wral df-rab sseq2i ssab dfss3 anbi1i
      r19.26 df-ral 3bitr2ri 3bitri ) DABCEZFDBGZCHZAIZBJZFUDDHUFKBLZDCFZABDMZI
      ZUCUGDABCNOUFBDPUKUEBDMZUJIUFBDMUHUIULUJBDCQRUEABDSUFBDTUAUB $.
      $( [8-Sep-2006] $) $( [16-Aug-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    ssrabdv.1 $e |- ( ph -> B (_ A ) $.
    ssrabdv.2 $e |- ( ( ph /\ x e. B ) -> ps ) $.
    $( Subclass of a restricted class abstraction (deduction rule). $)
    ssrabdv $p |- ( ph -> B (_ { x e. A | ps } ) $=
      ( wss wral wa crab r19.21aiva jca ssrab sylibr ) AEDHZBCEIZJEBCDKHAPQFABC
      EGLMBCDENO $.
      $( [13-Sep-2006] $) $( [31-Aug-2006] $)
  $}

  ${
    $d x ph $.
    ss2rabdv.1 $e |- ( ( ph /\ x e. A ) -> ( ps -> ch ) ) $.
    $( Deduction of restricted abstraction subclass from implication. $)
    ss2rabdv $p |- ( ph -> { x e. A | ps } (_ { x e. A | ch } ) $=
      ( wi wral crab wss r19.21aiva ss2rab sylibr ) ABCGZDEHBDEICDEIJANDEFKBCDE
      LM $.
      $( [31-May-2006] $) $( [30-May-2006] $)
  $}

  ${
    ss2rabi.1 $e |- ( x e. A -> ( ph -> ps ) ) $.
    $( Inference of restricted abstraction subclass from implication. $)
    ss2rabi $p |- { x e. A | ph } (_ { x e. A | ps } $=
      ( crab wss wi ss2rab mprgbir ) ACDFBCDFGABHCDABCDIEJ $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Subclass law for restricted abstraction. $)
    rabss2 $p |- ( A (_ B -> { x e. A | ph } (_ { x e. B | ph } ) $=
      ( cv wcel wi wal wa cab wss crab pm3.45 19.20i ss2ab sylibr dfss2 df-rab
      sseq12i 3imtr4i ) BEZCFZUADFZGZBHZUBAIZBJZUCAIZBJZKZCDKABCLZABDLZKUEUFUHG
      ZBHUJUDUMBUBUCAMNUFUHBOPBCDQUKUGULUIABCRABDRST $.
      $( [16-Jan-2005] $) $( [18-Dec-2004] $)

    $( Subclass relation for the restriction of a class abstraction. $)
    ssab2 $p |- { x | ( x e. A /\ ph ) } (_ A $=
      ( cv wcel wa pm3.26 abssi ) BDCEZAFBCIAGH $.
      $( [31-Mar-1995] $)

    $( Subclass relation for a restricted class. $)
    ssrab2 $p |- { x e. A | ph } (_ A $=
      ( crab cv wcel wa cab df-rab ssab2 eqsstri ) ABCDBECFAGBHCABCIABCJK $.
      $( [19-Mar-1997] $)
  $}

  ${
    $d w y z A $.  $d w y z B $.  $d w x z C $.  $d w D $.  $d x y $.
    $( A subset relationship useful for converting union to indexed union using
       ~ dfiun2 or ~ dfiun2g and intersection to indexed intersection using
       ~ dfiin2 . $)
    uniiunlem $p |- ( A. x e. A B e. D ->
                     ( A. x e. A B e. C <-> { y | E. x e. A y = B } (_ C ) ) $=
      ( vz vw wcel wral cv wceq wrex wi wal cab wss hbra1 ax-17 ra4 eleq1a
      syl6 r19.23ad 19.21aiv hbre1 hbim hbal wa csb wex csbeq1a eqcoms eqcomd
      eqeq1d eqid a4eiv visset hbcsb1 hbeleq eqeq1 rexbid eleq1 imbi12d cla4gv
      ra4e syl7 exp4a com4r imbi2d 3imtr3d 19.23aiv ax-mp imp3a com12 r19.20da
      impbid2 abss rexbidv cbvabv sseq1i bitr3i syl6bb ) DFIZACJZDEIZACJZGKZDLZ
      ACMZWGEIZNZGOZBKZDLZACMZBPZEQZWDWFWLWFWKGWFWHWJACWEACRWJASZWFAKZCIZWEWHWJ
      NWEACTDEWGUAUBUCUDWLWDWFWLWCWEACWKAGWIWJAWHACUEWRUFUGWCWLWTUHWEWCWLWTWEAH
      KZDUIZDLZHUJWCWLWTWENZNZNZXCDDLHAXAWSLZXBDDXGDXBDXBLWSXAAXADUKULUMUNDUOUP
      XCXFHXCXBFIZWLWTXBEIZNZNWCXEXHWLWTXCXIXHWLWTXCXIXHWLXCACMZXIWTXCUHWKXKXIN
      GXBFWGXBLZWIXKWJXIXLWHXCACAGXBAGXADHUQWGXAIASURUSWGXBDUTVAWGXBEVBVCVDXCAC
      VEVFVGVHXBDFVBXCXJXDWLXCXIWEWTXBDEVBVIVIVJVKVLVMVNVOVNVPWLWIGPZEQWQWIGEVQ
      XMWPEWIWOGBWGWMLWHWNACWGWMDUTVRVSVTWAWB $.
      $( [6-Oct-2006] $) $( [5-Oct-2006] $)
  $}

  $( Alternate definition of proper subclass. $)
  dfpss2 $p |- ( A (. B <-> ( A (_ B /\ -. A = B ) ) $=
    ( wpss wss wne wa wceq wn df-pss df-ne anbi2i bitri ) ABCABDZABEZFMABGHZFAB
    INOMABJKL $.
    $( [7-Feb-1996] $)

  $( Alternate definition of proper subclass. $)
  dfpss3 $p |- ( A (. B <-> ( A (_ B /\ -. B (_ A ) ) $=
    ( wss wceq wn wa wpss eqss notbii anbi2i dfpss2 wi anclb iman 3bitr3i
    con4bii 3bitr4i ) ABCZABDZEZFRRBACZFZEZFZABGRUAEFZTUCRSUBABHIJABKUEUDRUALRU
    BLUEEUDERUAMRUANRUBNOPQ $.
    $( [7-Feb-1996] $)

  $( Equality theorem for proper subclass. $)
  psseq1 $p |- ( A = B -> ( A (. C <-> B (. C ) ) $=
    ( wceq wss wne wa wpss sseq1 neeq1 anbi12d df-pss 3bitr4g ) ABDZACEZACFZGBC
    EZBCFZGACHBCHNOQPRABCIABCJKACLBCLM $.
    $( [7-Feb-1996] $)

  $( Equality theorem for proper subclass. $)
  psseq2 $p |- ( A = B -> ( C (. A <-> C (. B ) ) $=
    ( wceq wss wne wa wpss sseq2 neeq2 anbi12d df-pss 3bitr4g ) ABDZCAEZCAFZGCB
    EZCBFZGCAHCBHNOQPRABCIABCJKCALCBLM $.
    $( [7-Feb-1996] $)

  ${
    psseq1i.1 $e |- A = B $.
    $( An equality inference for the proper subclass relationship. $)
    psseq1i $p |- ( A (. C <-> B (. C ) $=
      ( wceq wpss wb psseq1 ax-mp ) ABEACFBCFGDABCHI $.
      $( [13-Jun-2004] $) $( [9-Jun-2004] $)

    $( An equality inference for the proper subclass relationship. $)
    psseq2i $p |- ( C (. A <-> C (. B ) $=
      ( wceq wpss wb psseq2 ax-mp ) ABECAFCBFGDABCHI $.
      $( [11-Jun-2004] $) $( [9-Jun-2004] $)

    ${
      psseq12i.2 $e |- C = D $.
      $( An equality inference for the proper subclass relationship. $)
      psseq12i $p |- ( A (. C <-> B (. D ) $=
        ( wpss psseq1i psseq2i bitri ) ACGBCGBDGABCEHCDBFIJ $.
        $( [15-Jun-2004] $) $( [9-Jun-2004] $)
    $}
  $}

  ${
    psseq1d.1 $e |- ( ph -> A = B ) $.
    $( An equality deduction for the proper subclass relationship. $)
    psseq1d $p |- ( ph -> ( A (. C <-> B (. C ) ) $=
      ( wceq wpss wb psseq1 syl ) ABCFBDGCDGHEBCDIJ $.
      $( [15-Jun-2004] $) $( [9-Jun-2004] $)

    $( An equality deduction for the proper subclass relationship. $)
    psseq2d $p |- ( ph -> ( C (. A <-> C (. B ) ) $=
      ( wceq wpss wb psseq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [11-Jun-2004] $) $( [9-Jun-2004] $)

    ${
      psseq12d.2 $e |- ( ph -> C = D ) $.
      $( An equality deduction for the proper subclass relationship. $)
      psseq12d $p |- ( ph -> ( A (. C <-> B (. D ) ) $=
        ( wpss psseq1d psseq2d bitrd ) ABDHCDHCEHABCDFIADECGJK $.
        $( [15-Jun-2004] $) $( [9-Jun-2004] $)
    $}
  $}

  $( A proper subclass is a subclass.  Theorem 10 of [Suppes] p. 23. $)
  pssss $p |- ( A (. B -> A (_ B ) $=
    ( wpss wss wne df-pss pm3.26bi ) ABCABDABEABFG $.
    $( [7-Feb-1996] $)

  ${
    pssssd.1 $e |- ( ph -> A (. B ) $.
    $( Deduce subclass from proper subclass. $)
    pssssd $p |- ( ph -> A (_ B ) $=
      ( wpss wss pssss syl ) ABCEBCFDBCGH $.
      $( [29-Feb-1996] $)
  $}

  $( Subclass in terms of proper subclass. $)
  sspss $p |- ( A (_ B <-> ( A (. B \/ A = B ) ) $=
    ( wss wpss wceq wo wn wa dfpss2 biimpri ex con1d orrd pssss eqimss jaoi
    impbii ) ABCZABDZABEZFRSTRTSRTGZSSRUAHABIJKLMSRTABNABOPQ $.
    $( [25-Feb-1996] $)

  $( Proper subclass is irreflexive.  Theorem 7 of [Suppes] p. 23. $)
  pssirr $p |- -. A (. A $=
    ( wpss wss wn wa pm3.24 dfpss3 mtbir ) AABAACZIDEIFAAGH $.
    $( [7-Feb-1996] $)

  $( Proper subclass has no 2-cycle loops.  Compare Theorem 8 of [Suppes]
     p. 23. $)
  pssn2lp $p |- -. ( A (. B /\ B (. A ) $=
    ( wpss wa wss wn pm3.24 dfpss3 anbi12i an42 bitri wo orc adantr ianor
    sylibr anim2i sylbi mto ) ABCZBACZDZABEZBAEZDZUEFZDZUEGUBUEUCFZUDFZDZDZUGUB
    UCUIDZUDUHDZDUKTULUAUMABHBAHIUCUIUDUHJKUJUFUEUJUHUILZUFUHUNUIUHUIMNUCUDOPQR
    S $.
    $( [7-Feb-1996] $)

  $( Two ways of stating trichotomy with respect to inclusion. $)
  sspsstri $p |- ( ( A (_ B \/ B (_ A ) <-> ( A (. B \/ A = B \/ B (. A ) ) $=
    ( wss wo wpss wceq w3o sspss eqcom orbi2i bitri orbi12i orordir or23
    df-3or bitr4i 3bitr2i ) ABCZBACZDABEZABFZDZBAEZUADZDTUCDUADZTUAUCGZRUBSUDAB
    HSUCBAFZDUDBAHUGUAUCBAIJKLTUCUAMUEUBUCDUFTUCUANTUAUCOPQ $.
    $( [13-Aug-2004] $) $( [12-Aug-2004] $)

  $( Partial trichotomy law for subclasses. $)
  ssnpss $p |- ( A (_ B -> -. B (. A ) $=
    ( wss wpss wceq wo wn sspss wi wa pssn2lp imnan mpbir pssirr psseq1 mtbii
    jaoi sylbi ) ABCABDZABEZFBADZGZABHSUBTSUBISUAJGABKSUALMTAADUAANABAOPQR $.
    $( [16-May-1996] $)

  $( Transitive law for proper subclass.  Theorem 9 of [Suppes] p. 23. $)
  psstr $p |- ( ( A (. B /\ B (. C ) -> A (. C ) $=
    ( wpss wa wss wceq wn pssss sylan9ss pssn2lp psseq1 anbi1d mtbiri con2i
    jca dfpss2 sylibr ) ABDZBCDZEZACFZACGZHZEACDUAUBUDSTABCABIBCIJUCUAUCUACBDZT
    ECBKUCSUETACBLMNOPACQR $.
    $( [7-Feb-1996] $)

  $( Transitive law for subclass and proper subclass. $)
  sspsstr $p |- ( ( A (_ B /\ B (. C ) -> A (. C ) $=
    ( wpss wceq wo wss wi psstr ex psseq1 biimprd jaoi imp sspss sylanb ) ABDZA
    BEZFZBCDZACDZABGSTUAQTUAHRQTUAABCIJRUATABCKLMNABOP $.
    $( [3-Apr-1996] $)


  $( Transitive law for subclass and proper subclass. $)
  psssstr $p |- ( ( A (. B /\ B (_ C ) -> A (. C ) $=
    ( wpss wceq wo wss psstr ex psseq2 biimpcd jaod imp sspss sylan2b ) ABDZBCD
    ZBCEZFZACDZBCGPSTPQTRPQTABCHIRPTBCAJKLMBCNO $.
    $( [3-Apr-1996] $)

