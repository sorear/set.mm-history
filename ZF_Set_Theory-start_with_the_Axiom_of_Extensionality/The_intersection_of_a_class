$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The intersection of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class intersection symbol. $)
  $c |^| $. $( Big cap $)

  $( Extend class notation to include the intersection of a class (read:
     'intersect ` A ` '). $)
  cint $a class |^| A $.

  ${
    $d x y A $.
    $( Define the intersection of a class.  Definition 7.35 of [TakeutiZaring]
       p. 44. $)
    df-int $a |- |^| A = { x | A. y ( y e. A -> x e. y ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class intersection. $)
    dfint2 $p |- |^| A = { x | A. y e. A x e. y } $=
      ( cint cv wcel wi wal cab wral df-int df-ral abbii eqtr4i ) CDBEZCFAEOFZG
      BHZAIPBCJZAIABCKRQAPBCLMN $.
      $( [28-Jun-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality law for intersection. $)
    inteq $p |- ( A = B -> |^| A = |^| B ) $=
      ( vx vy wceq cv wcel wral cab cint raleq abbidv dfint2 3eqtr4g ) ABEZCFDF
      GZDAHZCIPDBHZCIAJBJOQRCPDABKLCDAMCDBMN $.
      $( [13-Sep-1999] $)
  $}

  ${
    inteqi.1 $e |- A = B $.
    $( Equality inference for class intersection. $)
    inteqi $p |- |^| A = |^| B $=
      ( wceq cint inteq ax-mp ) ABDAEBEDCABFG $.
      $( [2-Sep-2003] $)
  $}

  ${
    inteqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for class intersection. $)
    inteqd $p |- ( ph -> |^| A = |^| B ) $=
      ( wceq cint inteq syl ) ABCEBFCFEDBCGH $.
      $( [2-Sep-2003] $)
  $}

  ${
    $d x A y $.  $d x B y $.
    elint.1 $e |- A e. _V $.
    $( Membership in class intersection. $)
    elint $p |- ( A e. |^| B <-> A. x ( x e. B -> A e. x ) ) $=
      ( vy cv wcel wi wal cint wceq eleq1 imbi2d albidv df-int elab2 ) AFZCGZEF
      ZQGZHZAIRBQGZHZAIEBCJDSBKZUAUCAUDTUBRSBQLMNEACOP $.
      $( [21-May-1994] $)
  $}

  ${
    $d x A $.  $d x B $.
    elint2.1 $e |- A e. _V $.
    $( Membership in class intersection. $)
    elint2 $p |- ( A e. |^| B <-> A. x e. B A e. x ) $=
      ( cint wcel cv wi wal wral elint df-ral bitr4i ) BCEFAGZCFBNFZHAIOACJABCD
      KOACLM $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Membership in class intersection, with the sethood requirement expressed
       as an antecedent. $)
    elintg $p |- ( A e. V -> ( A e. |^| B <-> A. x e. B A e. x ) ) $=
      ( vy cv cint wcel wral wb wceq eleq1 ralbidv bibi12d visset elint2
      vtoclg ) EFZCGZHZRAFZHZACIZJBSHZBUAHZACIZJEBDRBKZTUDUCUFRBSLUGUBUEACRBUAL
      MNARCEOPQ $.
      $( [20-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Membership in class intersection.  (The proof was shortened by Andrew
       Salmon, 9-Jul-2011.) $)
    elinti $p |- ( A e. |^| B -> ( C e. B -> A e. C ) ) $=
      ( vx cint wcel wi cv wral elintg wal df-ral wceq eleq1 eleq2 imbi12d
      cla4gv pm2.43b sylbi syl6bi pm2.43i ) ABEZFZCBFZACFZGZUCUCADHZFZDBIZUFDAB
      UBJUIUGBFZUHGZDKZUFUHDBLULUDUEUKUFDCBUGCMUJUDUHUEUGCBNUGCAOPQRSTUA $.
      $( [9-Jul-2011] $) $( [14-Oct-1999] $)
  $}

  ${
    $d y z A $.  $d x y z $.
    hbint.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for intersection.  (The proof was
       shortened by Andrew Salmon, 12-Aug-2011.) $)
    hbint $p |- ( y e. |^| A -> A. x y e. |^| A ) $=
      ( vz cv cint wcel wral visset elint2 hblem ax-17 hbral hbxfrbi ) BFZCGHPE
      FHZECIAEPCBJKQAECABECDLQAMNO $.
      $( [12-Aug-2011] $) $( [2-Feb-1997] $)
  $}

  ${
    $d A x y $.  $d ph y $.
    inteqab.1 $e |- A e. _V $.
    $( Membership in the intersection of a class abstraction. $)
    elintab $p |- ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) $=
      ( vy cab cint wcel cv wi wal elint hbab1 ax-17 hbim wceq eleq1 abid
      syl6bb eleq2 imbi12d cbval bitri ) CABFZGHEIZUDHZCUEHZJZEKACBIZHZJZBKECUD
      DLUHUKEBUFUGBABEMUGBNOUKENUEUIPZUFAUGUJULUFUIUDHAUEUIUDQABRSUEUICTUAUBUC
      $.
      $( [30-Aug-1993] $)

    $( Membership in the intersection of a class abstraction. $)
    elintrab $p |- ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) $=
      ( cv wcel wa cab cint wi wal crab wral elintab impexp albii bitri df-rab
      inteqi eleq2i df-ral 3bitr4i ) CBFZDGZAHZBIZJZGZUEACUDGZKZKZBLZCABDMZJZGU
      KBDNUIUFUJKZBLUMUFBCEOUPULBUEAUJPQRUOUHCUNUGABDSTUAUKBDUBUC $.
      $( [17-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d y ph $.
    $( Membership in the intersection of a class abstraction. $)
    elintrabg $p |- ( A e. V -> ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) ) $=
      ( vy cv crab cint wcel wi wral eleq1 wceq imbi2d ralbidv visset elintrab
      vtoclbg ) FGZABDHIZJATBGZJZKZBDLCUAJACUBJZKZBDLFCETCUAMTCNZUDUFBDUGUCUEAT
      CUBMOPABTDFQRS $.
      $( [17-Feb-2007] $)

    $( The intersection of the empty set is the universal class.  Exercise 2 of
       [TakeutiZaring] p. 44. $)
    int0 $p |- |^| (/) = _V $=
      ( vy vx cv c0 wcel wi wal cab wceq cint cvv noel pm2.21i ax-gen eqid 2th
      abbii df-int df-v 3eqtr4i ) ACZDEZBCZUAEZFZAGZBHUCUCIZBHDJKUFUGBUFUGUEAUB
      UDUALMNUCOPQBADRBST $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.
    $( An element of a class includes the intersection of the class.  Exercise
       4 of [TakeutiZaring] p. 44 (with correction), generalized to classes. $)
    intss1 $p |- ( A e. B -> |^| B C_ A ) $=
      ( vx vy wcel cint cv wi wal visset elint wceq eleq1 eleq2 imbi12d cla4gv
      pm2.43a syl5bi ssrdv ) ABEZCBFZACGZUAEDGZBEZUBUCEZHZDIZTUBAEZDUBBCJKUGTUH
      UFTUHHDABUCALUDTUEUHUCABMUCAUBNOPQRS $.
      $( [18-Nov-1995] $)

    $( Subclass of a class intersection.  Theorem 5.11(viii) of [Monk1] p. 52
       and its converse. $)
    ssint $p |- ( A C_ |^| B <-> A. x e. B A C_ x ) $=
      ( vy cint wss cv wcel wral dfss3 visset elint2 ralbii ralcom bitr4i
      3bitri ) BCEZFDGZQHZDBIRAGZHZACIZDBIZBTFZACIZDBQJSUBDBARCDKLMUCUADBIZACIU
      EUADABCNUDUFACDBTJMOP $.
      $( [14-Oct-1999] $)

    $( Subclass of the intersection of a class abstraction.  (The proof was
       shortened by Andrew Salmon, 9-Jul-2011.) $)
    ssintab $p |- ( A C_ |^| { x | ph } <-> A. x ( ph -> A C_ x ) ) $=
      ( vy cab cint wss cv wral wcel wi wal ssint df-ral hbab1 ax-17 hbim wceq
      eleq1 abid syl6bb sseq2 imbi12d cbval 3bitri ) CABEZFGCDHZGZDUFIUGUFJZUHK
      ZDLACBHZGZKZBLDCUFMUHDUFNUJUMDBUIUHBABDOUHBPQUMDPUGUKRZUIAUHULUNUIUKUFJAU
      GUKUFSABTUAUGUKCUBUCUDUE $.
      $( [9-Jul-2011] $) $( [31-Jul-2006] $)

    $( Subclass of a least upper bound. $)
    ssintub $p |- A C_ |^| { x e. B | A C_ x } $=
      ( vy cv wss crab cint ssint wcel sseq2 elrab simprbi mprgbir ) BBAEZFZACG
      ZHFBDEZFZDQDBQIRQJRCJSPSARCORBKLMN $.
      $( [8-Aug-2000] $)

    $( Subclass of the minimum value of class of supersets. $)
    ssmin $p |- A C_ |^| { x | ( A C_ x /\ ph ) } $=
      ( cv wss wa cab cint wi ssintab simpl mpgbir ) CCBDEZAFZBGHENMIBNBCJMAKL
      $.
      $( [10-Aug-2006] $)

    $( Any member of a class is the smallest of those members that include it.
       (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    intmin $p |- ( A e. B -> |^| { x e. B | A C_ x } = A ) $=
      ( vy wcel cv wss crab cint wi wral visset elintrab ssid wceq sseq2 eleq2
      imbi12d rcla4v mpii syl5bi ssrdv ssintub a1i eqssd ) BCEZBAFZGZACHIZBUFDU
      IBDFZUIEUHUJUGEZJZACKZUFUJBEZUHAUJCDLMUFUMBBGZUNBNULUOUNJABCUGBOUHUOUKUNU
      GBBPUGBUJQRSTUAUBBUIGUFABCUCUDUE $.
      $( [9-Jul-2011] $) $( [13-Aug-2002] $)

    $( Intersection of subclasses. $)
    intss $p |- ( A C_ B -> |^| B C_ |^| A ) $=
      ( vy vx cv wcel wi wal cint wss imim1 al2imi visset elint 3imtr4g
      19.21aiv dfss2 3imtr4i ) CEZAFZSBFZGZCHZDEZBIZFZUDAIZFZGZDHABJUEUGJUCUIDU
      CUAUDSFZGZCHTUJGZCHUFUHUBUKULCTUAUJKLCUDBDMZNCUDAUMNOPCABQDUEUGQR $.
      $( [14-Oct-1999] $)

    $( The intersection of a nonempty set is a subclass of its union. $)
    intssuni $p |- ( A =/= (/) -> |^| A C_ U. A ) $=
      ( vx vy c0 wne cint cuni cv wcel wral wrex r19.2z ex visset elint2
      eluni2 3imtr4g ssrdv ) ADEZBAFZAGZSBHZCHIZCAJZUCCAKZUBTIUBUAISUDUEUCCALMC
      UBABNOCUBAPQR $.
      $( [29-Jul-2006] $)
  $}

  $( If the union of a class is included in its intersection, the class is
     either the empty set or a singleton ( ~ uniintsn ).  (The proof was
     shortened by Andrew Salmon, 25-Jul-2011.) $)
  unissint $p |- ( U. A C_ |^| A <-> ( A = (/) \/ U. A = |^| A ) ) $=
    ( cuni cint wss c0 wceq wo wn wa simpl wne df-ne intssuni sylbir adantl
    eqssd ex orrd cvv ssv int0 sseqtr4i inteq sseq2d mpbiri eqimss jaoi impbii
    ) ABZACZDZAEFZUIUJFZGUKULUMUKULHZUMUKUNIUIUJUKUNJUNUJUIDZUKUNAEKUOAELAMNOPQ
    RULUKUMULUKUIECZDUISUPUITUAUBULUJUPUIAEUCUDUEUIUJUFUGUH $.
    $( [25-Jul-2011] $) $( [30-Oct-2010] $)

  $( Subclass relationship for intersection and union. $)
  intssuni2 $p |- ( ( A C_ B /\ A =/= (/) ) -> |^| A C_ U. B ) $=
    ( c0 wne wss cint cuni intssuni uniss sylan9ssr ) ACDABEAFAGBGAHABIJ $.
    $( [29-Jul-2006] $)

  ${
    $d x A $.  $d x B $.  $d x ps $.
    intminss.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Under subset ordering, the intersection of a restricted class
       abstraction is less than or equal to any of its members. $)
    intminss $p |- ( ( A e. B /\ ps ) -> |^| { x e. B | ph } C_ A ) $=
      ( wcel wa crab cint wss elrab intss1 sylbir ) DEGBHDACEIZGOJDKABCDEFLDOMN
      $.
      $( [7-Sep-2013] $)
  $}

  ${
    $d x A $.
    intmin2.1 $e |- A e. _V $.
    $( Any set is the smallest of all sets that include it. $)
    intmin2 $p |- |^| { x | A C_ x } = A $=
      ( cv wss cvv crab cint cab rabab inteqi wcel wceq intmin ax-mp eqtr3i )
      BADEZAFGZHZQAIZHBRTQAJKBFLSBMCABFNOP $.
      $( [30-Nov-2006] $) $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.  $d x ps $.
    intmin3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    intmin3.3 $e |- ps $.
    $( Under subset ordering, the intersection of a class abstraction is less
       than or equal to any of its members. $)
    intmin3 $p |- ( A e. V -> |^| { x | ph } C_ A ) $=
      ( wcel cab cint wss elabg mpbiri intss1 syl ) DEHZDACIZHZQJDKPRBGABCDEFLM
      DQNO $.
      $( [3-Jul-2005] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Elimination of a conjunct in a class intersection. $)
    intmin4 $p |- ( A C_ |^| { x | ph } ->
                  |^| { x | ( A C_ x /\ ph ) } = |^| { x | ph } ) $=
      ( vy cab cint wss cv wa wcel wi wal wb ssintab simpr ancr impbid2 imbi1d
      alimi albi syl sylbi visset elintab 3bitr4g eqrdv ) CABEFZGZDCBHZGZAIZBEF
      ZUGUHUKDHZUIJZKZBLZAUNKZBLZUMULJUMUGJUHAUJKZBLZUPURMZABCNUTUOUQMZBLVAUSVB
      BUSUKAUNUSUKAUJAOAUJPQRSUOUQBTUAUBUKBUMDUCZUDABUMVCUDUEUF $.
      $( [31-Jul-2006] $)
  $}

  ${
    $d x z A $.  $d x z ph $.  $d x y z $.
    intab.1 $e |- A e. _V $.
    intab.2 $e |- { x | E. y ( ph /\ x = A ) } e. _V $.
    $( The intersection of a special case of a class abstraction. ` y ` may be
       free in ` ph ` and ` A ` , which can be thought of a ` ph ( y ) ` and
       ` A ( y ) ` .  Typically, ~ abrexex2 or ~ abexssex can be used to
       satisfy the second hypothesis. $)
    intab $p |- |^| { x | A. y ( ph -> A e. x ) } =
                { x | E. y ( ph /\ x = A ) } $=
      ( vz cv wcel wi wal cab cint wceq wa wex wss wsbc cvv eqeq1 anbi2d
      exbidv cbvabv eqeltri hbe1 hbab hbeleq eleq2 imbi2d albid sbcie wb ax-17
      sbcgf ax-mp biimpri csb csbvarg sbceq1g mpbir jctir sbcang sylibr 19.8a
      ax-gen a4sbc mp2 sbcimg mpbi syl elabs mpgbir intss1 hba1 hbint ax-4
      com12 adantr eleq1 adantl sylibrd 19.21aiv visset elintab 19.23ai abssi
      eqssi eqtri ) ADBHZIZJZCKZBLZMZAGHZDNZOZCPZGLZAWIDNZOZCPZBLZWNWSWSWMIZWNW
      SQXDWLBWSRZXEADWSIZJZCWLXGCKBWSWSXCSWRXBGBWOWINZWQXACXHWPWTAWOWIDTUAUBUCZ
      FUDZWIWSNZWKXGCCBWSWRCGBWQCUEUFUGXKWJXFAWIWSDUHUIUJUKAWRGDRZXFAWQGDRZXLAA
      GDRZWPGDRZOZXMAXNXOXNADSIZXNAULEAGDSAGUMUNUOUPXOGDWOUQDNZXQXREGDSURUOXQXO
      XRULEGDWODSUSUOUTVAXQXMXPULEAWPGDSVBUOVCWQWRJZGDRZXMXLJZXQXSGKXTEXSGWQCVD
      VEXSGDSVFVGXQXTYAULEWQWRGDSVHUOVIVJWRGDEVKVCVLWLBWSXJVKUTWSWMVMUOWRGWNWQW
      OWNIZCCGWMWLCBGWKCVNUFVOWQWLWOWIIZJZBKYBWQYDBWQWLWJYCAWLWJJWPWLAWJWKCVPVQ
      VRWPYCWJULAWODWIVSVTWAWBWLBWOGWCWDVCWEWFWGXIWH $.
      $( [28-Jul-2006] $)
  $}

  $( The intersection of a class containing the empty set is empty. $)
  int0el $p |- ( (/) e. A -> |^| A = (/) ) $=
    ( c0 wcel cint intss1 wss 0ss a1i eqssd ) BACZADZBBAEBKFJKGHI $.
    $( [24-Apr-2004] $)

  ${
    $d x y A $.  $d x y B $.
    $( The class intersection of the union of two classes.  Theorem 78 of
       [Suppes] p. 42. $)
    intun $p |- |^| ( A u. B ) = ( |^| A i^i |^| B ) $=
      ( vx vy cun cint cin cv wcel wi wal wa 19.26 wo elun imbi1i jaob bitri
      albii visset elint anbi12i 3bitr4i elin eqriv ) CABEZFZAFZBFZGZDHZUFIZCHZ
      UKIZJZDKZUMUHIZUMUIIZLZUMUGIUMUJIUKAIZUNJZUKBIZUNJZLZDKVADKZVCDKZLUPUSVAV
      CDMUOVDDUOUTVBNZUNJVDULVGUNUKABOPUTUNVBQRSUQVEURVFDUMACTZUADUMBVHUAUBUCDU
      MUFVHUAUMUHUIUDUCUE $.
      $( [22-Sep-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    intpr.1 $e |- A e. _V $.
    intpr.2 $e |- B e. _V $.
    $( The intersection of a pair is the intersection of its members.  Theorem
       71 of [Suppes] p. 42. $)
    intpr $p |- |^| { A , B } = ( A i^i B ) $=
      ( vx vy cpr cint cin cv wcel wi wal wa wceq 19.26 wo visset elpr imbi1i
      jaob bitri albii clel4 anbi12i 3bitr4i elint elin eqriv ) EABGZHZABIZFJZU
      JKZEJZUMKZLZFMZUOAKZUOBKZNZUOUKKUOULKUMAOZUPLZUMBOZUPLZNZFMVCFMZVEFMZNURV
      AVCVEFPUQVFFUQVBVDQZUPLVFUNVIUPUMABFRSTVBUPVDUAUBUCUSVGUTVHFUOACUDFUOBDUD
      UEUFFUOUJERUGUOABUHUFUI $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( The intersection of a pair is the intersection of its members.  Closed
       form of ~ intpr .  Theorem 71 of [Suppes] p. 42.  (Contributed by FL,
       27-Apr-2008.) $)
    intprg $p |- ( ( A e. V /\ B e. W ) -> |^| { A , B } = ( A i^i B ) ) $=
      ( vx vy cv cpr cint cin wceq preq1 inteqd ineq1 eqeq12d preq2 ineq2
      visset intpr vtocl2g ) EGZFGZHZIZUAUBJZKAUBHZIZAUBJZKABHZIZABJZKEFABCDUAA
      KZUDUGUEUHULUCUFUAAUBLMUAAUBNOUBBKZUGUJUHUKUMUFUIUBBAPMUBBAQOUAUBERFRST
      $.
      $( [6-Mar-2011] $) $( [27-Apr-2008] $)
  $}

  ${
    intsn.1 $e |- A e. _V $.
    $( The intersection of a singleton is its member.  Theorem 70 of [Suppes]
       p. 41. $)
    intsn $p |- |^| { A } = A $=
      ( csn cint cpr cin dfsn2 inteqi intpr inidm 3eqtri ) ACZDAAEZDAAFALMAGHAA
      BBIAJK $.
      $( [29-Sep-2002] $)
  $}

  ${
    $d x y A $.
    $( Two ways to express " ` A ` is a singleton."  See also ~ en1 , ~ card1 ,
       and ~ eusn . $)
    uniintsn $p |- ( U. A = |^| A <-> E. x A = { x } ) $=
      ( vy cuni cint wceq cv csn wex wcel wa wi wal c0 wne cvv vn0 inteq int0
      syl6eq adantl unieq uni0 eqeq1 syl5ib imp eqtr3d ex necon3d mpi n0 sylib
      cpr wss visset prss cun cin uniss simpl sseqtrd intss sstrd unipr intpr
      3sstr3g inss1 ssun1 sstri jctir eqss uneqin bitr3i syl5bi 19.21aivv jca
      weu cab euabsn eleq1 eu4 abid2 eqeq1i exbii 3bitr3i unisn intsn 3eqtr4a
      19.23aiv impbii ) BDZBEZFZBAGZHZFZAIZWMWNBJZAIZWRCGZBJZKZWNWTFZLZCMAMZKZW
      QWMWSXEWMBNOZWSWMPNOXGQWMBNPNWMBNFZPNFWMXHKWLPNXHWLPFWMXHWLNEPBNRSTUAWMXH
      WLNFZXHWKNFWMXIXHWKNDNBNUBUCTWKWLNUDUEUFUGUHUIUJABUKULWMXDACXBWNWTUMZBUNZ
      WMXCWNWTBAUOZCUOZUPWMXKXCWMXKKZWNWTUQZWNWTURZUNZXPXOUNZKZXCXNXQXRXNXJDZXJ
      EZXOXPXNXTWLYAXNXTWKWLXKXTWKUNWMXJBUSUAWMXKUTVAXKWLYAUNWMXJBVBUAVCWNWTXLX
      MVDWNWTXLXMVEVFXPWNXOWNWTVGWNWTVHVIVJXSXOXPFXCXOXPVKWNWTVLVMULUHVNVOVPWRA
      VQWRAVRZWOFZAIXFWQWRAVSWRXAACWNWTBVTWAYCWPAYBBWOABWBWCWDWEULWPWMAWPWODWNW
      KWLWNXLWFBWOUBWPWLWOEWNBWORWNXLWGTWHWIWJ $.
      $( [2-Aug-2010] $)
  $}

  ${
    intunsn.1 $e |- B e. _V $.
    $( Theorem joining a singleton to an intersection. $)
    intunsn $p |- |^| ( A u. { B } ) = ( |^| A i^i B ) $=
      ( csn cun cint cin intun intsn ineq2i eqtri ) ABDZEFAFZLFZGMBGALHNBMBCIJK
      $.
      $( [29-Sep-2002] $)
  $}

