$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The intersection of a class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare class intersection symbol. $)
  $c |^| $. $( Big cap $)

  $( Extend class notation to include the intersection of a class (read:
     'intersect ` A ` '). $)
  cint $a class |^| A $.

  ${
    $d x y A $.
    $( Define the intersection of a class.  Definition 7.35 of
       [TakeutiZaring] p. 44. $)
    df-int $a |- |^| A = { x | A. y ( y e. A -> x e. y ) } $.
  $}

  ${
    $d x y A $.
    $( Alternate definition of class intersection. $)
    dfint2 $p |- |^| A = { x | A. y e. A x e. y } $=
      ( cint cv wcel wi wal cab wral df-int df-ral abbii eqtr4 ) CDBEZCFAEOFZGB
      HZAIPBCJZAIABCKRQAPBCLMN $.
      $( [28-Jun-1998] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality law for intersection. $)
    inteq $p |- ( A = B -> |^| A = |^| B ) $=
      ( vx vy wceq cv wcel wral cab cint raleq1 abbidv dfint2 3eqtr4g ) ABEZCFD
      FGZDAHZCIPDBHZCIAJBJOQRCPDABKLCDAMCDBMN $.
      $( [13-Sep-1999] $)
  $}

  ${
    inteqi.1 $e |- A = B $.
    $( Equality inference for class intersection. $)
    inteqi $p |- |^| A = |^| B $=
      ( wceq cint inteq ax-mp ) ABDAEBEDCABFG $.
      $( [2-Sep-2003] $)
  $}

  ${
    inteqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for class intersection. $)
    inteqd $p |- ( ph -> |^| A = |^| B ) $=
      ( wceq cint inteq syl ) ABCEBFCFEDBCGH $.
      $( [2-Sep-2003] $)
  $}

  ${
    $d x A y $.  $d x B y $.
    elint.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint $p |- ( A e. |^| B <-> A. x ( x e. B -> A e. x ) ) $=
      ( vy cv wcel wi wal cint wceq eleq1 imbi2d albidv df-int elab2 ) AFZCGZEF
      ZQGZHZAIRBQGZHZAIEBCJDSBKZUAUCAUDTUBRSBQLMNEACOP $.
      $( [21-May-1994] $)
  $}

  ${
    $d x A $.  $d x B $.
    elint2.1 $e |- A e. V $.
    $( Membership in class intersection. $)
    elint2 $p |- ( A e. |^| B <-> A. x e. B A e. x ) $=
      ( cint wcel cv wi wal wral elint df-ral bitr4 ) BCEFAGZCFBNFZHAIOACJABCDK
      OACLM $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Membership in class intersection, with the sethood requirement expressed
       as an antecedent. $)
    elintg $p |- ( A e. C -> ( A e. |^| B <-> A. x e. B A e. x ) ) $=
      ( vy cv cint wcel wral wb wceq eleq1 ralbidv bibi12d visset elint2
      vtoclg ) EFZCGZHZRAFZHZACIZJBSHZBUAHZACIZJEBDRBKZTUDUCUFRBSLUGUBUEACRBUAL
      MNARCEOPQ $.
      $( [21-Nov-2003] $) $( [20-Nov-2003] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Membership in class intersection. $)
    elinti $p |- ( A e. |^| B -> ( C e. B -> A e. C ) ) $=
      ( vy vx cint wcel cv wi wal wceq eleq1 imbi2d albidv visset elint biimp
      vtoclga eleq2 imbi12d cla4gv pm2.43b syl ) ABFZGDHZBGZAUEGZIZDJZCBGZACGZI
      ZUFEHZUEGZIZDJZUIEAUDUMAKZUOUHDUQUNUGUFUMAUELMNUMUDGUPDUMBEOPQRUIUJUKUHUL
      DCBUECKUFUJUGUKUECBLUECASTUAUBUC $.
      $( [14-Oct-1999] $)
  $}

  ${
    $d y z A $.  $d x y z $.
    hbint.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for intersection. $)
    hbint $p |- ( y e. |^| A -> A. x y e. |^| A ) $=
      ( vz cv wcel wi wal cint ax-17 hbel hbim hbal visset elint albii 3imtr4
      ) EFZCGZBFZSGZHZEIZUDAIUACJGZUEAIUCAETUBAABBSCUBAKZDLUFMNEUACBOPZUEUDAUGQ
      R $.
      $( [2-Feb-1997] $)
  $}

  ${
    $d A x y $.  $d ph y $.
    inteqab.1 $e |- A e. V $.
    $( Membership in the intersection of a class abstraction. $)
    elintab $p |- ( A e. |^| { x | ph } <-> A. x ( ph -> A e. x ) ) $=
      ( vy cab cint wcel cv wi wal elint hbab1 ax-17 hbim wceq eleq1 abid
      syl6bb eleq2 imbi12d cbval bitr ) CABFZGHEIZUDHZCUEHZJZEKACBIZHZJZBKECUDD
      LUHUKEBUFUGBABEMUGBNOUKENUEUIPZUFAUGUJULUFUIUDHAUEUIUDQABRSUEUICTUAUBUC
      $.
      $( [30-Aug-1993] $)

    $( Membership in the intersection of a class abstraction. $)
    elintrab $p |- ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) $=
      ( cv wcel wa cab cint wi wal crab wral elintab impexp albii bitr df-rab
      inteqi eleq2i df-ral 3bitr4 ) CBFZDGZAHZBIZJZGZUEACUDGZKZKZBLZCABDMZJZGUK
      BDNUIUFUJKZBLUMUFBCEOUPULBUEAUJPQRUOUHCUNUGABDSTUAUKBDUBUC $.
      $( [17-Oct-1999] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d y ph $.
    $( Membership in the intersection of a class abstraction. $)
    elintrabg $p |- ( A e. C -> ( A e. |^| { x e. B | ph } <->
                 A. x e. B ( ph -> A e. x ) ) ) $=
      ( vy cv crab cint wcel wi wral eleq1 wceq imbi2d ralbidv visset elintrab
      vtoclbg ) FGZABDHIZJATBGZJZKZBDLCUAJACUBJZKZBDLFCETCUAMTCNZUDUFBDUGUCUEAT
      CUBMOPABTDFQRS $.
      $( [28-Feb-2007] $) $( [17-Feb-2007] $)

    $( The intersection of the empty set is the universal class.  Exercise 2 of
       [TakeutiZaring] p. 44. $)
    int0 $p |- |^| (/) = V $=
      ( vy vx cv c0 wcel wi wal cab wceq cint cvv noel pm2.21i ax-gen eqid 2th
      abbii df-int df-v 3eqtr4 ) ACZDEZBCZUAEZFZAGZBHUCUCIZBHDJKUFUGBUFUGUEAUBU
      DUALMNUCOPQBADRBST $.
      $( [18-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.
    $( An element of a class includes the intersection of the class.  Exercise
       4 of [TakeutiZaring] p. 44 (with correction), generalized to classes. $)
    intss1 $p |- ( A e. B -> |^| B (_ A ) $=
      ( vx vy wcel cint cv wi wal wceq eleq1 eleq2 imbi12d cla4gv pm2.43a
      visset elint syl5ib ssrdv ) ABEZCBFZATDGZBEZCGZUBEZHZDIZUDAEZUDUAEUGTUHUF
      TUHHDABUBAJUCTUEUHUBABKUBAUDLMNODUDBCPQRS $.
      $( [18-Nov-1995] $)

    $( Subclass of a class intersection.  Theorem 5.11(viii) of [Monk1] p. 52
       and its converse. $)
    ssint $p |- ( A (_ |^| B <-> A. x e. B A (_ x ) $=
      ( vy cint wss cv wcel wral dfss3 visset elint2 ralbii ralcom bitr4 3bitr
      ) BCEZFDGZQHZDBIRAGZHZACIZDBIZBTFZACIZDBQJSUBDBARCDKLMUCUADBIZACIUEUADABC
      NUDUFACDBTJMOP $.
      $( [14-Oct-1999] $)

    $( Subclass of the intersection of a class abstraction. $)
    ssintab $p |- ( A (_ |^| { x | ph } <-> A. x ( ph -> A (_ x ) ) $=
      ( vy cab cint wss cv wral wcel wi wal ssint df-ral hbab1 ax-17 hbim wceq
      eleq1 sseq2 imbi12d cbval abid imbi1i albii bitr 3bitr ) CABEZFGCDHZGZDUH
      IUIUHJZUJKZDLZACBHZGZKZBLZDCUHMUJDUHNUMUNUHJZUOKZBLUQULUSDBUKUJBABDOUJBPQ
      USDPUIUNRUKURUJUOUIUNUHSUIUNCTUAUBUSUPBURAUOABUCUDUEUFUG $.
      $( [31-Jul-2006] $) $( [31-Jul-2006] $)

    $( Subclass of a least upper bound. $)
    ssintub $p |- A (_ |^| { x e. B | A (_ x } $=
      ( vy cv wss crab cint ssint wcel sseq2 elrab pm3.27bi mprgbir ) BBAEZFZAC
      GZHFBDEZFZDQDBQIRQJRCJSPSARCORBKLMN $.
      $( [8-Aug-2000] $)

    $( Subclass of the minimum value of class of supersets. $)
    ssmin $p |- A (_ |^| { x | ( A (_ x /\ ph ) } $=
      ( cv wss wa cab cint wi ssintab pm3.26 mpgbir ) CCBDEZAFZBGHENMIBNBCJMAKL
      $.
      $( [10-Aug-2006] $) $( [10-Aug-2006] $)

    $( Any member of a class is the smallest of those members that include
       it. $)
    intmin $p |- ( A e. B -> |^| { x e. B | A (_ x } = A ) $=
      ( vy wcel cv wss crab cint wa wceq wi wral ssid sseq2 eleq2 imbi12d
      rcla4v mpii visset elintrab syl5ib ssrdv ssintub jctir eqss sylibr ) BCEZ
      BAFZGZACHIZBGZBUKGZJUKBKUHULUMUHDUKBUHUJDFZUIEZLZACMZUNBEZUNUKEUHUQBBGZUR
      BNUPUSURLABCUIBKUJUSUOURUIBBOUIBUNPQRSUJAUNCDTUAUBUCABCUDUEUKBUFUG $.
      $( [8-Oct-2006] $) $( [13-Aug-2002] $)

    $( Intersection of subclasses. $)
    intss $p |- ( A (_ B -> |^| B (_ |^| A ) $=
      ( vy vx cv wcel wi wal cint wss imim1 19.20ii visset elint 3imtr4g
      19.21aiv dfss2 3imtr4 ) CEZAFZSBFZGZCHZDEZBIZFZUDAIZFZGZDHABJUEUGJUCUIDUC
      UAUDSFZGZCHTUJGZCHUFUHUBUKULCTUAUJKLCUDBDMZNCUDAUMNOPCABQDUEUGQR $.
      $( [14-Oct-1999] $)

    $( The intersection of a nonempty set is a subclass of its union. $)
    intssuni $p |- ( A =/= (/) -> |^| A (_ U. A ) $=
      ( vx vy c0 wne cint cuni cv wcel wral wrex r19.2z ex visset elint2
      eluni2 3imtr4g ssrdv ) ADEZBAFZAGZSBHZCHIZCAJZUCCAKZUBTIUBUAISUDUEUCCALMC
      UBABNOCUBAPQR $.
      $( [21-Dec-2006] $) $( [29-Jul-2006] $)
  $}

  $( Subclass relationship for intersection and union. $)
  intssuni2 $p |- ( ( A (_ B /\ A =/= (/) ) -> |^| A (_ U. B ) $=
    ( c0 wne wss cint cuni intssuni uniss sylan9ssr ) ACDABEAFAGBGAHABIJ $.
    $( [31-Dec-2006] $) $( [29-Jul-2006] $)

  ${
    $d x A $.
    intmin2.1 $e |- A e. V $.
    $( Any set is the smallest of all sets that include it. $)
    intmin2 $p |- |^| { x | A (_ x } = A $=
      ( cv wss cvv crab cint cab rabab inteqi wcel wceq intmin ax-mp eqtr3 ) BA
      DEZAFGZHZQAIZHBRTQAJKBFLSBMCABFNOP $.
      $( [30-Nov-2006] $) $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.  $d x ps $.
    intmin3.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    intmin3.3 $e |- ps $.
    $( Under subset ordering, the intersection of a class abstraction is less
       than or equal to any of its members. $)
    intmin3 $p |- ( A e. B -> |^| { x | ph } (_ A ) $=
      ( wcel cab cint wss elabg mpbiri intss1 syl ) DEHZDACIZHZQJDKPRBGABCDEFLM
      DQNO $.
      $( [6-Jul-2005] $) $( [3-Jul-2005] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Elimination of a conjunct in a class intersection. $)
    intmin4 $p |- ( A (_ |^| { x | ph } ->
                  |^| { x | ( A (_ x /\ ph ) } = |^| { x | ph } ) $=
      ( vy cab cint wss cv wa wcel wi wal wb ssintab pm3.27 ancr impbid2
      imbi1d 19.20i 19.15 syl sylbi visset elintab 3bitr4g eqrdv ) CABEFZGZDCBH
      ZGZAIZBEFZUGUHUKDHZUIJZKZBLZAUNKZBLZUMULJUMUGJUHAUJKZBLZUPURMZABCNUTUOUQM
      ZBLVAUSVBBUSUKAUNUSUKAUJAOAUJPQRSUOUQBTUAUBUKBUMDUCZUDABUMVCUDUEUF $.
      $( [11-Aug-2006] $) $( [31-Jul-2006] $)
  $}

  ${
    $d x z A $.  $d x z ph $.  $d x y z $.
    intab.1 $e |- A e. V $.
    intab.2 $e |- { x | E. y ( ph /\ x = A ) } e. V $.
    $( The intersection of a special case of a class abstraction.  ` y ` may be
       free in ` ph ` and ` A ` , which can be thought of a ` ph ( y ) ` and
       ` A ( y ) ` .  Typically, ~ abrexex2 or ~ abexssex can be used to
       satisfy the second hypothesis. $)
    intab $p |- |^| { x | A. y ( ph -> A e. x ) } =
                { x | E. y ( ph /\ x = A ) } $=
      ( vz cv wcel wi wal cab cint wceq wa wex wss wsbc cvv eqeq1 anbi2d
      exbidv cbvabv eqeltr hbe1 hbab hbeleq eleq2 imbi2d albid sbcie wb ax-17
      sbcgf ax-mp biimpr csb csbvarg sbceq1dig mpbir jctir sbcang sylibr 19.8a
      ax-gen a4sbc mp2 sbcimg mpbi syl elabs mpgbir intss1 hba1 hbint ax-4
      com12 adantr eleq1 adantl sylibrd 19.21aiv visset elintab 19.23ai abssi
      eqssi eqtr ) ADBHZIZJZCKZBLZMZAGHZDNZOZCPZGLZAWIDNZOZCPZBLZWNWSWSWMIZWNWS
      QXDWLBWSRZXEADWSIZJZCWLXGCKBWSWSXCSWRXBGBWOWINZWQXACXHWPWTAWOWIDTUAUBUCZF
      UDZWIWSNZWKXGCCBWSWRCGBWQCUEUFUGXKWJXFAWIWSDUHUIUJUKAWRGDRZXFAWQGDRZXLAAG
      DRZWPGDRZOZXMAXNXOXNADSIZXNAULEAGDSAGUMUNUOUPXOGDWOUQDNZXQXREGDSURUOXQXOX
      RULEGDWODSUSUOUTVAXQXMXPULEAWPGDSVBUOVCWQWRJZGDRZXMXLJZXQXSGKXTEXSGWQCVDV
      EXSGDSVFVGXQXTYAULEWQWRGDSVHUOVIVJWRGDEVKVCVLWLBWSXJVKUTWSWMVMUOWRGWNWQWO
      WNIZCCGWMWLCBGWKCVNUFVOWQWLWOWIIZJZBKYBWQYDBWQWLWJYCAWLWJJWPWLAWJWKCVPVQV
      RWPYCWJULAWODWIVSVTWAWBWLBWOGWCWDVCWEWFWGXIWH $.
      $( [7-Aug-2006] $) $( [28-Jul-2006] $)
  $}

  $( The intersection of a class containing the empty set is empty. $)
  int0el $p |- ( (/) e. A -> |^| A = (/) ) $=
    ( c0 wcel cint intss1 wss 0ss a1i eqssd ) BACZADZBBAEBKFJKGHI $.
    $( [25-Apr-2004] $) $( [24-Apr-2004] $)

  ${
    $d x y A $.  $d x y B $.
    $( The class intersection of the union of two classes.  Theorem 78 of
       [Suppes] p. 42. $)
    intun $p |- |^| ( A u. B ) = ( |^| A i^i |^| B ) $=
      ( vx vy cun cint cin cv wcel wi wal wa 19.26 wo elun imbi1i jaob bitr
      albii visset elint anbi12i 3bitr4 elin eqriv ) CABEZFZAFZBFZGZDHZUFIZCHZU
      KIZJZDKZUMUHIZUMUIIZLZUMUGIUMUJIUKAIZUNJZUKBIZUNJZLZDKVADKZVCDKZLUPUSVAVC
      DMUOVDDUOUTVBNZUNJVDULVGUNUKABOPUTUNVBQRSUQVEURVFDUMACTZUADUMBVHUAUBUCDUM
      UFVHUAUMUHUIUDUCUE $.
      $( [22-Sep-2002] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    intpr.1 $e |- A e. V $.
    intpr.2 $e |- B e. V $.
    $( The intersection of a pair is the intersection of its members.  Theorem
       71 of [Suppes] p. 42. $)
    intpr $p |- |^| { A , B } = ( A i^i B ) $=
      ( vx vy cpr cint cin cv wcel wi wal wa wceq 19.26 wo visset elpr imbi1i
      jaob bitr albii clel4 anbi12i 3bitr4 elint elin eqriv ) EABGZHZABIZFJZUJK
      ZEJZUMKZLZFMZUOAKZUOBKZNZUOUKKUOULKUMAOZUPLZUMBOZUPLZNZFMVCFMZVEFMZNURVAV
      CVEFPUQVFFUQVBVDQZUPLVFUNVIUPUMABFRSTVBUPVDUAUBUCUSVGUTVHFUOACUDFUOBDUDUE
      UFFUOUJERUGUOABUHUFUI $.
      $( [14-Oct-1999] $)
  $}

  ${
    intsn.1 $e |- A e. V $.
    $( The intersection of a singleton is its member.  Theorem 70 of [Suppes]
       p. 41. $)
    intsn $p |- |^| { A } = A $=
      ( csn cint cpr cin dfsn2 inteqi intpr inidm 3eqtr ) ACZDAAEZDAAFALMAGHAAB
      BIAJK $.
      $( [29-Sep-2002] $)
  $}

  ${
    intunsn.1 $e |- B e. V $.
    $( Theorem joining a singleton to an intersection. $)
    intunsn $p |- |^| ( A u. { B } ) = ( |^| A i^i B ) $=
      ( csn cun cint cin intun intsn ineq2i eqtr ) ABDZEFAFZLFZGMBGALHNBMBCIJK
      $.
      $( [29-Sep-2002] $)
  $}

