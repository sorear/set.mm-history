$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           The empty set
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for the empty or null set. $)
  $c (/) $. $( null set $)

  $( Extend class notation to include the empty set. $)
  c0 $a class (/) $.

  $( Define the empty set.  Special case of Exercise 4.10(o) of [Mendelson]
     p. 231.  For a more traditional definition, but requiring a dummy
     variable, see ~ dfnul2 . $)
  df-nul $a |- (/) = ( _V \ _V ) $.

  $( Alternate definition of the empty set.  Definition 5.14 of [TakeutiZaring]
     p. 20. $)
  dfnul2 $p |- (/) = { x | -. x = x } $=
    ( cv wceq wn c0 wcel cvv cdif wa df-nul eleq2i eldif eqid pm3.24 2th
    con2bii 3bitri abbi2i ) ABZSCZDZAESEFSGGHZFSGFZUCDIZUAEUBSJKSGGLTUDTUDDSMUC
    NOPQR $.
    $( [26-Dec-1996] $)

  $( Alternate definition of the empty set.. $)
  dfnul3 $p |- (/) = { x e. A | -. x e. A } $=
    ( cv wceq wn cab wcel wa c0 crab pm3.24 eqid 2th con1bii abbii dfnul2
    df-rab 3eqtr4i ) ACZSDZEZAFSBGZUBEZHZAFIUCABJUAUDAUDTUDETUBKSLMNOAPUCABQR
    $.
    $( [25-Mar-2004] $)

  ${
    $d A x $.
    $( The empty set has no elements.  Theorem 6.14 of [Quine] p. 44. $)
    noel $p |- -. A e. (/) $=
      ( vx cvv wcel c0 wn cv wceq eqid dfnul2 abeq2i con2bii mpbi eleq1 mtbii
      vtocleg elisset con3i pm2.61i ) ACDZAEDZFZUBBACBGZAHUCEDZUAUCUCHZUDFUCIUD
      UEUEFBEBJKLMUCAENOPUATAEQRS $.
      $( [5-Aug-1993] $)
  $}

  $( If a set has elements, it is not empty. $)
  n0i $p |- ( B e. A -> -. A = (/) ) $=
    ( c0 wceq wcel noel eleq2 mtbiri con2i ) ACDZBAEZJKBCEBFACBGHI $.
    $( [31-Dec-1993] $)

  $( If a set has elements, it is not empty. $)
  ne0i $p |- ( B e. A -> A =/= (/) ) $=
    ( wcel c0 wceq wn wne n0i df-ne sylibr ) BACADEFADGABHADIJ $.
    $( [31-Dec-1993] $)

  $( The universal class is not equal to the empty set. $)
  vn0 $p |- _V =/= (/) $=
    ( vx cv cvv wcel c0 wne visset ne0i ax-mp ) ABZCDCEFAGCJHI $.
    $( [11-Sep-2008] $)

  ${
    $d x y $.  $d y A $.
    ne0f.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       [TakeutiZaring] p. 20.  This version of ~ n0 requires only that ` x `
       not be free in, rather than not occur in, ` A ` . $)
    ne0f $p |- ( A =/= (/) <-> E. x x e. A ) $=
      ( c0 wceq wn cv wcel wal wne wex wb ax-17 cleqf noel nbn albii bitr4i
      notbii df-ne df-ex 3bitr4i ) CEFZGAHZCIZGZAJZGCEKUFALUDUHUDUFUEEIZMZAJUHA
      BCEDBHEIANOUGUJAUIUFUEPQRSTCEUAUFAUBUC $.
      $( [2-Apr-2007] $) $( [17-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       [TakeutiZaring] p. 20. $)
    n0 $p |- ( A =/= (/) <-> E. x x e. A ) $=
      ( vy cv wcel ax-17 ne0f ) ACBCDBEAFG $.
      $( [29-Sep-2006] $)

    $( A nonempty class has at least one element.  Proposition 5.17(1) of
       [TakeutiZaring] p. 20. $)
    neq0 $p |- ( -. A = (/) <-> E. x x e. A ) $=
      ( c0 wceq wn wne cv wcel wex df-ne n0 bitr3i ) BCDEBCFAGBHAIBCJABKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x ph $.
    reximdva0.1 $e |- ( ( ph /\ x e. A ) -> ps ) $.
    $( Restricted existence deduced from non-empty class. $)
    reximdva0 $p |- ( ( ph /\ A =/= (/) ) -> E. x e. A ps ) $=
      ( c0 wne wa cv wcel wex wrex ex ancld eximdv imp n0 sylan2b df-rex
      sylibr ) ADFGZHCIDJZBHZCKZBCDLAUBCKZUDUAAUEUDAUBUCCAUBBAUBBEMNOPCDQRBCDST
      $.
      $( [1-Feb-2012] $)
  $}

  ${
    $d A x $.
    $( A case of equivalence of "at most one" and "only one".  (Contributed by
       FL, 6-Dec-2010.) $)
    n0moeu $p |- ( A =/= (/) -> ( E* x x e. A <-> E! x x e. A ) ) $=
      ( c0 wne cv wcel wmo wex wa weu n0 biimpi biantrurd eu5 syl6bbr ) BCDZAEB
      FZAGZQAHZRIQAJPSRPSABKLMQANO $.
      $( [2-Mar-2011] $) $( [14-Nov-2010] $)
  $}

  $( Vacuous existential quantification is false. $)
  rex0 $p |- -. E. x e. (/) ph $=
    ( c0 cv wcel wn noel pm2.21i nrex ) ABCBDZCEAFJGHI $.
    $( [15-Oct-2003] $)

  ${
    $d x A $.
    $( The empty set has no elements.  Theorem 2 of [Suppes] p. 22. $)
    eq0 $p |- ( A = (/) <-> A. x -. x e. A ) $=
      ( c0 wceq cv wcel wn wal wex neq0 df-ex bitri con4bii ) BCDZAEBFZGAHZNGOA
      IPGABJOAKLM $.
      $( [29-Aug-1993] $)

    $( The universe contains every set. $)
    eqv $p |- ( A = _V <-> A. x x e. A ) $=
      ( cvv wceq cv wcel wb wal dfcleq visset tbt albii bitr4i ) BCDAEZBFZNCFZG
      ZAHOAHABCIOQAPOAJKLM $.
      $( [11-Sep-2006] $)
  $}

  ${
    $d x A $.  $d x y $.
    $( Membership of the empty set in another class. $)
    0el $p |- ( (/) e. A <-> E. x e. A A. y -. y e. x ) $=
      ( c0 wcel cv wceq wrex wn wal risset eq0 rexbii bitri ) DCEAFZDGZACHBFOEI
      BJZACHADCKPQACBOLMN $.
      $( [29-Jun-2004] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( Nonempty class abstraction. $)
    abn0 $p |- ( { x | ph } =/= (/) <-> E. x ph ) $=
      ( vy cab c0 wne cv wcel wex n0 hbab1 ax-17 eleq1 cbvex abid exbii 3bitri
      ) ABDZEFCGZRHZCIBGZRHZBIABICRJTUBCBABCKUBCLSUARMNUBABABOPQ $.
      $( [21-Feb-2007] $) $( [26-Dec-1996] $)
  $}

  $( Non-empty restricted class abstraction. $)
  rabn0 $p |- ( { x e. A | ph } =/= (/) <-> E. x e. A ph ) $=
    ( cv wcel wa cab c0 wne wex crab wrex abn0 df-rab neeq1i df-rex 3bitr4i )
    BDCEAFZBGZHIRBJABCKZHIABCLRBMTSHABCNOABCPQ $.
    $( [24-Feb-2007] $) $( [29-Aug-1999] $)

  $( Any restricted class abstraction restricted to the empty set is empty.
     (The proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
  rab0 $p |- { x e. (/) | ph } = (/) $=
    ( cv c0 wcel wa cab wceq wn crab equid noel intnanr 2th con2bii abbii
    df-rab dfnul2 3eqtr4i ) BCZDEZAFZBGTTHZIZBGABDJDUBUDBUCUBUCUBIBKUAATLMNOPAB
    DQBRS $.
    $( [26-Jun-2011] $) $( [15-Oct-2003] $)

  $( Any restricted class abstraction restricted to the empty set is empty. $)
  rab0OLD $p |- { x e. (/) | ph } = (/) $=
    ( cv c0 wcel wa wex wn crab wceq noel intnanr nex wne wrex rabn0 df-rex
    bitri necon1bbii mpbi ) BCZDEZAFZBGZHABDIZDJUCBUBAUAKLMUDUEDUEDNABDOUDABDPA
    BDQRST $.
    $( [15-Oct-2003] $)

  ${
    $d x A $.
    $( The union of a class with the empty set is itself.  Theorem 24 of
       [Suppes] p. 27. $)
    un0 $p |- ( A u. (/) ) = A $=
      ( vx c0 cv wcel wo noel biorfi bicomi uneqri ) BACABDZAEZLKCEZFMLKGHIJ $.
      $( [5-Aug-1993] $)

    $( The intersection of a class with the empty set is the empty set.
       Theorem 16 of [Suppes] p. 26. $)
    in0 $p |- ( A i^i (/) ) = (/) $=
      ( vx c0 cv wcel wa noel bianfi bicomi ineqri ) BACCBDZCEZKAEZLFLMKGHIJ $.
      $( [5-Aug-1993] $)
  $}

  $( The intersection of a class with the universal class is itself.  Exercise
     4.10(k) of [Mendelson] p. 231. $)
  inv1 $p |- ( A i^i _V ) = A $=
    ( cvv cin inss1 ssid ssv ssini eqssi ) ABCAABDAABAEAFGH $.
    $( [17-May-1998] $)

  $( The union of a class with the universal class is the universal class.
     Exercise 4.10(l) of [Mendelson] p. 231. $)
  unv $p |- ( A u. _V ) = _V $=
    ( cvv cun ssv ssun2 eqssi ) ABCZBGDBAEF $.
    $( [17-May-1998] $)

  ${
    $d A x $.
    $( The null set is a subset of any class.  Part of Exercise 1 of
       [TakeutiZaring] p. 22. $)
    0ss $p |- (/) C_ A $=
      ( vx c0 cv wcel noel pm2.21i ssriv ) BCABDZCEIAEIFGH $.
      $( [5-Aug-1993] $)
  $}

  $( Any subset of the empty set is empty.  Theorem 5 of [Suppes] p. 23 and its
     converse. $)
  ss0b $p |- ( A C_ (/) <-> A = (/) ) $=
    ( c0 wceq wss eqss 0ss mpbiran2 bicomi ) ABCZABDZIJBADABEAFGH $.
    $( [17-Sep-2003] $)

  $( Any subset of the empty set is empty.  Theorem 5 of [Suppes] p. 23. $)
  ss0 $p |- ( A C_ (/) -> A = (/) ) $=
    ( c0 wss wceq ss0b biimpi ) ABCABDAEF $.
    $( [13-Aug-1994] $)

  $( A subclass of an empty class is empty.  (The proof was shortened by Andrew
     Salmon, 26-Jun-2011.) $)
  sseq0 $p |- ( ( A C_ B /\ B = (/) ) -> A = (/) ) $=
    ( c0 wceq wss sseq2 ss0 syl6bi impcom ) BCDZABEZACDZJKACELBCAFAGHI $.
    $( [26-Jun-2011] $) $( [7-Mar-2007] $)

  $( A subclass of an empty class is empty. $)
  sseq0OLD $p |- ( ( A C_ B /\ B = (/) ) -> A = (/) ) $=
    ( wss c0 wceq wa sseq2 biimpac ss0b sylib ) ABCZBDEZFADCZADELKMBDAGHAIJ $.
    $( [7-Mar-2007] $)

  $( A class with a nonempty subclass is nonempty. $)
  ssn0 $p |- ( ( A C_ B /\ A =/= (/) ) -> B =/= (/) ) $=
    ( wss c0 wne wceq sseq0 ex necon3d imp ) ABCZADEBDEKBDADKBDFADFABGHIJ $.
    $( [17-Feb-2007] $)

  ${
    abf.1 $e |- -. ph $.
    $( A class builder with a false argument is empty. $)
    abf $p |- { x | ph } = (/) $=
      ( cab c0 wss wceq cv wcel pm2.21i abssi ss0 ax-mp ) ABDZEFNEGABEABHEICJKN
      LM $.
      $( [20-Jan-2012] $)
  $}

  $( Two classes are empty iff their union is empty. $)
  un00 $p |- ( ( A = (/) /\ B = (/) ) <-> ( A u. B ) = (/) ) $=
    ( c0 wceq wa cun uneq12 un0 syl6eq wss ssun1 sseq2 mpbii ss0b sylib ssun2
    jca impbii ) ACDZBCDZEZABFZCDZUAUBCCFCACBCGCHIUCSTUCACJZSUCAUBJUDABKUBCALMA
    NOUCBCJZTUCBUBJUEBAPUBCBLMBNOQR $.
    $( [11-Aug-2004] $)

  $( Only the universal class has the universal class as a subclass.  (The
     proof was shortened by Andrew Salmon, 26-Jun-2011.) $)
  vss $p |- ( _V C_ A <-> A = _V ) $=
    ( cvv wss wa wceq ssv biantrur eqss bitr4i ) BACZABCZJDABEKJAFGABHI $.
    $( [26-Jun-2011] $) $( [17-Sep-2003] $)

  $( Only the universal class has the universal class as a subclass. $)
  vssOLD $p |- ( _V C_ A <-> A = _V ) $=
    ( cvv wss wceq wa ssv jctl eqss sylibr eqimss2 impbii ) BACZABDZLABCZLEMLNA
    FGABHIBAJK $.
    $( [17-Sep-2003] $)

  $( The null set is a proper subset of any non-empty set. $)
  0pss $p |- ( (/) C. A <-> A =/= (/) ) $=
    ( c0 wpss wne wss df-pss 0ss mpbiran necom bitri ) BACZBADZABDKBAELBAFAGHBA
    IJ $.
    $( [2-Apr-2007] $) $( [27-Feb-1996] $)

  $( No set is a proper subset of the empty set.  (The proof was shortened by
     Andrew Salmon, 26-Jun-2011.) $)
  npss0 $p |- -. A C. (/) $=
    ( c0 wpss wss wn wa wi 0ss a1i iman mpbi dfpss3 mtbir ) ABCABDZBADZEFZNOGPE
    ONAHINOJKABLM $.
    $( [26-Jun-2011] $) $( [17-Jun-1998] $)

  $( No set is a proper subset of the empty set. $)
  npss0OLD $p |- -. A C. (/) $=
    ( c0 wpss wceq eqid wn wss wb pssss ss0 psseq1 3syl ibi wne 0pss df-ne
    bitri sylib mt2 ) ABCZBBDZBETBBCZUAFZTUBTABGABDTUBHABIAJABBKLMUBBBNUCBOBBPQ
    RS $.
    $( [17-Jun-1998] $)

  $( Any non-universal class is a proper subclass of the universal class. $)
  pssv $p |- ( A C. _V <-> -. A = _V ) $=
    ( cvv wpss wss wceq wn dfpss2 ssv mpbiran ) ABCABDABEFABGAHI $.
    $( [17-May-1998] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj $p |- ( ( A i^i B ) = (/) <-> A. x e. A -. x e. B ) $=
      ( cin c0 wceq cv wcel wn wi wal wral wa cab wb df-in eqeq1i abeq1 imnan
      noel nbn bitr2i albii 3bitri df-ral bitr4i ) BCDZEFZAGZBHZUICHZIZJZAKZULA
      BLUHUJUKMZANZEFUOUIEHZOZAKUNUGUPEABCPQUOAERURUMAUMUOIURUJUKSUQUOUITUAUBUC
      UDULABUEUF $.
      $( [17-Feb-2004] $)

    $( Two ways of saying that two classes are disjoint (have no members in
       common). $)
    disj1 $p |- ( ( A i^i B ) = (/) <-> A. x ( x e. A -> -. x e. B ) ) $=
      ( cin c0 wceq cv wcel wn wral wi wal disj df-ral bitri ) BCDEFAGZCHIZABJP
      BHQKALABCMQABNO $.
      $( [19-Aug-1993] $)

    $( Two ways of saying that two classes are disjoint, using the complement
       of ` B ` relative to a universe ` C ` .  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    reldisj $p |- ( A C_ C -> ( ( A i^i B ) = (/) <-> A C_ ( C \ B ) ) ) $=
      ( vx wss cv wcel wn wi wal cdif cin c0 wceq wb dfss2 wa pm5.44 eldif
      imbi2i syl6bbr a4s sylbi albidv disj1 3bitr4g ) ACEZDFZAGZUHBGHZIZDJUIUHC
      BKZGZIZDJABLMNAULEUGUKUNDUGUIUHCGZIZDJUKUNOZDACPUPUQDUPUKUIUOUJQZIUNUIUOU
      JRUMURUIUHCBSTUAUBUCUDDABUEDAULPUF $.
      $( [26-Jun-2011] $) $( [15-Feb-2007] $)

    $( Two ways of saying that two classes are disjoint, using the complement
       of ` B ` relative to a universe ` C ` . $)
    reldisjOLD $p |- ( A C_ C -> ( ( A i^i B ) = (/) <-> A C_ ( C \ B ) ) ) $=
      ( vx wss cv wcel wn wi wal cdif cin c0 wceq wa ssel biantrurd pm4.76
      eldif imbi2i bitr4i syl6bb albidv disj1 dfss2 3bitr4g ) ACEZDFZAGZUHBGHZI
      ZDJUIUHCBKZGZIZDJABLMNAULEUGUKUNDUGUKUIUHCGZIZUKOZUNUGUPUKACUHPQUQUIUOUJO
      ZIUNUIUOUJRUMURUIUHCBSTUAUBUCDABUDDAULUEUF $.
      $( [15-Feb-2007] $)

    $( Two ways of saying that two classes are disjoint. $)
    disj3 $p |- ( ( A i^i B ) = (/) <-> A = ( A \ B ) ) $=
      ( vx cv wcel wn wi wal cdif wb cin c0 wceq wa pm4.71 eldif bibi2i bitr4i
      albii disj1 dfcleq 3bitr4i ) CDZAEZUCBEFZGZCHUDUCABIZEZJZCHABKLMAUGMUFUIC
      UFUDUDUENZJUIUDUEOUHUJUDUCABPQRSCABTCAUGUAUB $.
      $( [19-May-1998] $)

    $( Members of disjoint sets are not equal.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    disjne $p |- ( ( ( A i^i B ) = (/) /\ C e. A /\ D e. B ) -> C =/= D ) $=
      ( vx cin c0 wceq wcel wne cv wn wral wi wa eleq1 biimprd com12 necon3bd
      notbid rcla4cva syl5com disj sylanb 3impia ) ABFGHZCAIZDBIZCDJZEKZBIZLZEA
      MZUGUHUINUFUHCBIZLZUIUMUGOUHUNCDCDHZUHUNUPUNUHCDBPQRSULUOECAUJCHUKUNUJCBP
      TUAUBEABUCUDUE $.
      $( [26-Jun-2011] $) $( [28-Mar-2007] $)

    $( Members of disjoint sets are not equal. $)
    disjneOLD $p |- ( ( ( A i^i B ) = (/) /\ C e. A /\ D e. B ) ->
                   C =/= D ) $=
      ( vx cin c0 wceq wcel wne cv wn wral wi wa nelneq df-ne sylibr necomd ex
      eleq1 notbid rcla4cva syl5com disj sylanb 3impia ) ABFGHZCAIZDBIZCDJZEKZB
      IZLZEAMZUIUJUKNUHUJCBIZLZUKUOUIOUJUQUKUJUQOZDCURDCHLDCJDCBPDCQRSTUNUQECAU
      LCHUMUPULCBUAUBUCUDEABUEUFUG $.
      $( [28-Mar-2007] $)
  $}

  $( Two ways of saying that two classes are disjoint. $)
  disj2 $p |- ( ( A i^i B ) = (/) <-> A C_ ( _V \ B ) ) $=
    ( cvv wss cin c0 wceq cdif wb ssv reldisj ax-mp ) ACDABEFGACBHDIAJABCKL $.
    $( [17-May-1998] $)

  $( Two ways of saying that two classes are disjoint. $)
  disj4 $p |- ( ( A i^i B ) = (/) <-> -. ( A \ B ) C. A ) $=
    ( cin c0 wceq cdif wpss wn disj3 eqcom wss dfpss2 difss mpbiran con2bii
    3bitri ) ABCDEAABFZEQAEZQAGZHABIAQJSRSQAKRHQALABMNOP $.
    $( [21-Mar-2004] $)

  $( Intersection with a subclass of a disjoint class.  (Contributed by FL,
     24-Jan-2007.) $)
  ssdisj $p |- ( ( A C_ B /\ ( B i^i C ) = (/) ) -> ( A i^i C ) = (/) ) $=
    ( wss cin c0 wceq wa wi ssrin sstr2 syl ss0b syl5ibr imp ss0 ) ABDZBCEZFGZH
    ACEZFDZTFGQSUAQRFDZUASQTRDUBUAIABCJTRFKLRMNOTPL $.
    $( [23-Jan-2007] $)

  $( A class is a proper subset of its union with a disjoint nonempty class. $)
  disjpss $p |- ( ( ( A i^i B ) = (/) /\ B =/= (/) ) -> A C. ( A u. B ) ) $=
    ( cin c0 wceq wne wa wss wn cun wpss sseq2 ssid biantru ssin bitri syl5bb
    ss0 syl6bi necon3ad imp nsspssun uncom psseq2i sylib ) ABCZDEZBDFZGBAHZIZAA
    BJZKZUGUHUJUGUIBDUGUIBDHZBDEUGBUFHZUMUIUFDBLUIUIBBHZGUNUOUIBMNBABOPQBRSTUAU
    JABAJZKULBAUBUPUKABAUCUDPUE $.
    $( [30-Apr-2007] $) $( [15-Sep-2004] $)

  $( The union of disjoint classes is disjoint. $)
  undisj1 $p |- ( ( ( A i^i C ) = (/) /\ ( B i^i C ) = (/) ) <->
               ( ( A u. B ) i^i C ) = (/) ) $=
    ( cin c0 wceq wa cun un00 indir eqeq1i bitr4i ) ACDZEFBCDZEFGMNHZEFABHCDZEF
    MNIPOEABCJKL $.
    $( [26-Sep-2004] $)

  $( The union of disjoint classes is disjoint. $)
  undisj2 $p |- ( ( ( A i^i B ) = (/) /\ ( A i^i C ) = (/) ) <->
               ( A i^i ( B u. C ) ) = (/) ) $=
    ( cin c0 wceq wa cun un00 indi eqeq1i bitr4i ) ABDZEFACDZEFGMNHZEFABCHDZEFM
    NIPOEABCJKL $.
    $( [13-Sep-2004] $)

  $( Subclass expressed in terms of intersection with difference from the
     universal class. $)
  ssindif0 $p |- ( A C_ B <-> ( A i^i ( _V \ B ) ) = (/) ) $=
    ( cvv cdif cin c0 wceq wss disj2 ddif sseq2i bitr2i ) ACBDZEFGACMDZHABHAMIN
    BABJKL $.
    $( [17-Sep-2003] $)

  $( The intersection of classes with a common member is nonempty. $)
  inelcm $p |- ( ( A e. B /\ A e. C ) -> ( B i^i C ) =/= (/) ) $=
    ( wcel wa cin c0 wne elin ne0i sylbir ) ABDACDEABCFZDLGHABCILAJK $.
    $( [2-Apr-2007] $) $( [7-Apr-1994] $)

  $( A minimum element of a class has no elements in common with the class. $)
  minel $p |- ( ( A e. B /\ ( C i^i B ) = (/) ) -> -. A e. C ) $=
    ( cin c0 wceq wcel wn wa wi inelcm necon2bi imnan sylibr con2d impcom ) CBD
    ZEFZABGZACGZHRTSRTSIZHTSHJUAQEACBKLTSMNOP $.
    $( [22-Jun-1994] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Distribute union over difference.  (The proof was shortened by Andrew
       Salmon, 26-Jun-2011.) $)
    undif4 $p |- ( ( A i^i C ) = (/) ->
                 ( A u. ( B \ C ) ) = ( ( A u. B ) \ C ) ) $=
      ( vx cv wcel wn wi wal cdif cun wb cin c0 wceq wo wa pm2.621 olc impbid1
      anbi2d eldif orbi2i ordi bitri elun anbi1i 3bitr4g alimi disj1 dfcleq
      3imtr4i ) DEZAFZUMCFGZHZDIUMABCJZKZFZUMABKZCJZFZLZDIACMNOURVAOUPVCDUPUNUM
      UQFZPZUMUTFZUOQZUSVBUPUNUMBFZPZUNUOPZQZVIUOQVEVGUPVJUOVIUPVJUOUNUORUOUNST
      UAVEUNVHUOQZPVKVDVLUNUMBCUBUCUNVHUOUDUEVFVIUOUMABUFUGUHUMAUQUFUMUTCUBUHUI
      DACUJDURVAUKUL $.
      $( [26-Jun-2011] $) $( [17-May-1998] $)

    $( Distribute union over difference. $)
    undif4OLD $p |- ( ( A i^i C ) = (/) ->
                 ( A u. ( B \ C ) ) = ( ( A u. B ) \ C ) ) $=
      ( vx cv wcel wn wi wal cdif cun wb cin c0 wceq wo wa pm2.61 ax-1 impbid1
      df-or syl5bb anbi2d eldif orbi2i ordi bitri elun anbi1i 3bitr4g alimi
      disj1 dfcleq 3imtr4i ) DEZAFZUOCFGZHZDIUOABCJZKZFZUOABKZCJZFZLZDIACMNOUTV
      COURVEDURUPUOUSFZPZUOVBFZUQQZVAVDURUPUOBFZPZUPUQPZQZVKUQQVGVIURVLUQVKURUP
      GZUQHZUQVLURVOUQUPUQRUQVNSTUPUQUAUBUCVGUPVJUQQZPVMVFVPUPUOBCUDUEUPVJUQUFU
      GVHVKUQUOABUHUIUJUOAUSUHUOVBCUDUJUKDACULDUTVCUMUN $.
      $( [17-May-1998] $)

    $( Subset relation for disjoint classes.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    disjssun $p |- ( ( A i^i B ) = (/) -> ( A C_ ( B u. C ) <-> A C_ C ) ) $=
      ( cin c0 wceq cun wss uneq2 un0 syl6eq indi uncom eqtri syl5eq eqeq1d
      df-ss 3bitr4g ) ABDZEFZABCGZDZAFACDZAFAUAHACHTUBUCATUCSGZUCUBTUDUCEGUCSEU
      CIUCJKUBSUCGUDABCLSUCMNOPAUAQACQR $.
      $( [26-Jun-2011] $) $( [25-Oct-2005] $)

    $( Subset relation for disjoint classes. $)
    disjssunOLD $p |- ( ( A i^i B ) = (/) -> ( A C_ ( B u. C ) <-> A C_ C ) )
        $=
      ( vx cin c0 wceq cv cun wcel wral wss wa wo wn wb wi wal disj1 ax-4
      sylbi imp biorf syl elun syl6rbbr ralbidva dfss3 3bitr4g ) ABEFGZDHZBCIZJ
      ZDAKUKCJZDAKAULLACLUJUMUNDAUJUKAJZMZUNUKBJZUNNZUMUPUQOZUNURPUJUOUSUJUOUSQ
      ZDRUTDABSUTDTUAUBUQUNUCUDUKBCUEUFUGDAULUHDACUHUI $.
      $( [25-Oct-2005] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Subclass expressed in terms of difference.  Exercise 7 of
       [TakeutiZaring] p. 22. $)
    ssdif0 $p |- ( A C_ B <-> ( A \ B ) = (/) ) $=
      ( vx cv wcel wi wal cdif wn wss c0 wceq wa iman eldif notbii bitr4i
      albii dfss2 eq0 3bitr4i ) CDZAEZUBBEZFZCGUBABHZEZIZCGABJUFKLUEUHCUEUCUDIM
      ZIUHUCUDNUGUIUBABOPQRCABSCUFTUA $.
      $( [29-Apr-1994] $)
  $}

  $( Universal class equality in terms of empty difference. $)
  vdif0 $p |- ( A = _V <-> ( _V \ A ) = (/) ) $=
    ( cvv wceq wss cdif c0 vss ssdif0 bitr3i ) ABCBADBAEFCAGBAHI $.
    $( [17-Sep-2003] $)

  $( A proper subclass has a nonempty difference. $)
  pssdifn0 $p |- ( ( A C_ B /\ A =/= B ) -> ( B \ A ) =/= (/) ) $=
    ( wss wne cdif c0 wceq eqss simplbi2 ssdif0 syl5ibr necon3d imp ) ABCZABDBA
    EZFDNOFABNBACZABGZOFGQNPABHIBAJKLM $.
    $( [3-Apr-2007] $) $( [3-May-1994] $)

  $( A subclass missing a member is a proper subclass. $)
  ssnelpss $p |- ( A C_ B -> ( ( C e. B /\ -. C e. A ) -> A C. B ) ) $=
    ( wss wceq wn wpss wcel wa dfpss2 baibr nelneq2 eqcom notbii sylib syl5bi
    ) ABDZABEZFZABGZCBHCAHFIZTQSABJKUABAEZFSCBALUBRBAMNOP $.
    $( [12-Jan-2002] $)

  ${
    $d x A $.  $d x B $.
    $( A proper subclass has a member in one argument that's not in both. $)
    pssnel $p |- ( A C. B -> E. x ( x e. B /\ -. x e. A ) ) $=
      ( wpss cv cdif wcel wex wn wa c0 wne wss df-pss pssdifn0 sylbi n0 sylib
      eldif exbii ) BCDZAEZCBFZGZAHZUBCGUBBGIJZAHUAUCKLZUEUABCMBCLJUGBCNBCOPAUC
      QRUDUFAUBCBSTR $.
      $( [29-Feb-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Difference, intersection, and subclass relationship. $)
    difin0ss $p |- ( ( ( A \ B ) i^i C ) = (/) -> ( C C_ A -> C C_ B ) ) $=
      ( vx cdif cin c0 wceq cv wcel wi wal wss wn eq0 wa annim anbi2i ancom
      bitr3i notbii iman elin eldif anbi1i bitri 3bitr4i ax-2 sylbir al2imi
      sylbi dfss2 3imtr4g ) ABEZCFZGHZDIZCJZUQAJZKZDLZURUQBJZKZDLZCAMCBMUPUQUOJ
      ZNZDLVAVDKDUOOVFUTVCDVFURUSVBKZKZUTVCKURVGNZPZNUSVBNPZURPZNVHVFVJVLVJURVK
      PVLVKVIURUSVBQRURVKSTUAURVGUBVEVLVEUQUNJZURPVLUQUNCUCVMVKURUQABUDUEUFUAUG
      URUSVBUHUIUJUKDCAULDCBULUM $.
      $( [30-Apr-1994] $)

    $( Intersection, subclass, and difference relationship.  (The proof was
       shortened by Andrew Salmon, 26-Jun-2011.) $)
    inssdif0 $p |- ( ( A i^i B ) C_ C <-> ( A i^i ( B \ C ) ) = (/) ) $=
      ( vx cv cin wcel wi wal cdif wn wss c0 wceq wa iman anass notbii bitri
      elin imbi1i eldif anbi2i 3bitr4i albii dfss2 eq0 ) DEZABFZGZUHCGZHZDIUHAB
      CJZFZGZKZDIUICLUNMNULUPDUHAGZUHBGZOZUKHZUQURUKKZOZOZKZULUPUTUSVAOZKVDUSUK
      PVEVCUQURVAQRSUJUSUKUHABTUAUOVCUOUQUHUMGZOVCUHAUMTVFVBUQUHBCUBUCSRUDUEDUI
      CUFDUNUGUD $.
      $( [26-Jun-2011] $) $( [27-Oct-1996] $)

    $( Intersection, subclass, and difference relationship. $)
    inssdif0OLD $p |- ( ( A i^i B ) C_ C <-> ( A i^i ( B \ C ) ) = (/) ) $=
      ( vx cv cin wcel wi wal cdif wn wss c0 wceq wa impexp iman imbi2i imnan
      3bitri elin imbi1i eldif anbi2i bitri notbii 3bitr4i albii dfss2 eq0 ) DE
      ZABFZGZUKCGZHZDIUKABCJZFZGZKZDIULCLUQMNUOUSDUKAGZUKBGZOZUNHZUTVAUNKOZOZKZ
      UOUSVCUTVAUNHZHUTVDKZHVFUTVAUNPVGVHUTVAUNQRUTVDSTUMVBUNUKABUAUBURVEURUTUK
      UPGZOVEUKAUPUAVIVDUTUKBCUCUDUEUFUGUHDULCUIDUQUJUG $.
      $( [27-Oct-1996] $)
  $}

  $( The difference between a class and itself is the empty set.  Proposition
     5.15 of [TakeutiZaring] p. 20.  Also Theorem 32 of [Suppes] p. 28. $)
  difid $p |- ( A \ A ) = (/) $=
    ( wss cdif c0 wceq ssid ssdif0 mpbi ) AABAACDEAFAAGH $.
    $( [22-Apr-2004] $)

  ${
    $d x A $.
    $( The difference between a class and itself is the empty set.  Proposition
       5.15 of [TakeutiZaring] p. 20.  Also Theorem 32 of [Suppes] p. 28.
       (Alternate proof of ~ difid suggested by David Abernethy,
       17-Jun-2012.) $)
    difidALT $p |- ( A \ A ) = (/) $=
      ( vx cdif cv wcel wn crab c0 dfdif2 dfnul3 eqtr4i ) AACBDAEFBAGHBAAIBAJK
      $.
      $( [17-Jun-2012] $)
  $}

  $( The difference between a class and the empty set.  Part of Exercise 4.4 of
     [Stoll] p. 16. $)
  dif0 $p |- ( A \ (/) ) = A $=
    ( cdif c0 difid difeq2i difdif eqtr3i ) AAABZBACBAHCAADEAAFG $.
    $( [17-Aug-2004] $)

  $( The difference between the empty set and a class.  Part of Exercise 4.4 of
     [Stoll] p. 16. $)
  0dif $p |- ( (/) \ A ) = (/) $=
    ( c0 cdif wss wceq difss ss0 ax-mp ) BACZBDIBEBAFIGH $.
    $( [17-Aug-2004] $)

  $( A class and its relative complement are disjoint.  Theorem 38 of [Suppes]
     p. 29. $)
  difdisj $p |- ( A i^i ( B \ A ) ) = (/) $=
    ( cin wss cdif c0 wceq inss1 inssdif0 mpbi ) ABCADABAECFGABHABAIJ $.
    $( [24-Mar-1998] $)

  $( The difference of a class from its intersection is empty.  Theorem 37 of
     [Suppes] p. 29.  (The proof was shortened by Andrew Salmon,
     26-Jun-2011.) $)
  difin0 $p |- ( ( A i^i B ) \ B ) = (/) $=
    ( cin wss cdif c0 wceq inss2 ssdif0 mpbi ) ABCZBDKBEFGABHKBIJ $.
    $( [26-Jun-2011] $) $( [17-Aug-2004] $)

  $( The difference of a class from its intersection is empty.  Theorem 37 of
     [Suppes] p. 29. $)
  difin0OLD $p |- ( ( A i^i B ) \ B ) = (/) $=
    ( cin cdif c0 difindir difid ineq2i in0 3eqtri ) ABCBDABDZBBDZCKECEABBFLEKB
    GHKIJ $.
    $( [17-Aug-2004] $)

  $( The union of a class and its complement is the universe.  Theorem 5.1(5)
     of [Stoll] p. 17. $)
  undifv $p |- ( A u. ( _V \ A ) ) = _V $=
    ( cvv cdif cun cin c0 dfun3 difdisj difeq2i dif0 3eqtri ) ABACZDBLBLCEZCBFC
    BALGMFBLBHIBJK $.
    $( [17-Aug-2004] $)

  $( Absorption of difference by union.  This decomposes a union into two
     disjoint classes (see ~ difdisj ).  Theorem 35 of [Suppes] p. 29. $)
  undif1 $p |- ( ( A \ B ) u. B ) = ( A u. B ) $=
    ( cvv cdif cin cun invdif uneq1i undir uncom undifv eqtri ineq2i inv1
    3eqtri eqtr3i ) ACBDZEZBFZABDZBFABFZRTBABGHSUAQBFZEUACEUAAQBIUBCUAUBBQFCQBJ
    BKLMUANOP $.
    $( [18-Aug-2004] $) $( [19-May-1998] $)

  $( Absorption of difference by union.  This decomposes a union into two
     disjoint classes (see ~ difdisj ).  Part of proof of Corollary 6K of
     [Enderton] p. 144. $)
  undif2 $p |- ( A u. ( B \ A ) ) = ( A u. B ) $=
    ( cdif cun uncom undif1 3eqtri ) ABACZDHADBADABDAHEBAFBAEG $.
    $( [4-Sep-2004] $) $( [19-May-1998] $)
  ${
    $d x A $.  $d x B $.
    $( The intersection and class difference of a class with another class
       unite to give the original class.  (Contributed by Paul Chapman,
       5-Jun-2009.)  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    inundif $p |- ( ( A i^i B ) u. ( A \ B ) ) = A $=
      ( vx cin cdif cv wcel wo wa wn elin eldif orbi12i pm4.42 bitr4i uneqri )
      CABDZABEZACFZQGZSRGZHSAGZSBGZIZUBUCJIZHUBTUDUAUESABKSABLMUBUCNOP $.
      $( [26-Jun-2011] $) $( [5-Jun-2009] $)
  $}

  $( The intersection and class difference of a class with another class unite
     to give the original class.  (Contributed by Paul Chapman, 5-Jun-2009.) $)
  inundifOLD $p |- ( ( A i^i B ) u. ( A \ B ) ) = A $=
    ( cin cdif cun undir wss wceq difss ssequn2 mpbi undif2 uncom eqtri
    ineq12i inabs 3eqtri ) ABCABDZEAREZBREZCAABEZCAABRFSATUARAGSAHABIRAJKTBAEUA
    BALBAMNOABPQ $.
    $( [5-Jun-2009] $)

  $( Absorption of union by difference.  Theorem 36 of [Suppes] p. 29. $)
  difun2 $p |- ( ( A u. B ) \ B ) = ( A \ B ) $=
    ( cun cdif c0 difundir difid uneq2i un0 3eqtri ) ABCBDABDZBBDZCKECKABBFLEKB
    GHKIJ $.
    $( [18-Aug-2004] $) $( [19-May-1998] $)

  $( Union of complementary parts into whole. $)
  undif $p |- ( A C_ B <-> ( A u. ( B \ A ) ) = B ) $=
    ( wss cun wceq cdif ssequn1 undif2 eqeq1i bitr4i ) ABCABDZBEABAFDZBEABGLKBA
    BHIJ $.
    $( [22-Sep-2004] $) $( [22-Mar-1998] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( A condition equivalent to inclusion in the union of two classes. $)
    ssundif $p |- ( A C_ ( B u. C ) <-> ( A \ B ) C_ C ) $=
      ( vx cv wcel cun wi wal cdif wss wn wa wo pm5.6 eldif imbi1i elun imbi2i
      3bitr4ri albii dfss2 3bitr4i ) DEZAFZUDBCGZFZHZDIUDABJZFZUDCFZHZDIAUFKUIC
      KUHULDUEUDBFZLMZUKHUEUMUKNZHULUHUEUMUKOUJUNUKUDABPQUGUOUEUDBCRSTUADAUFUBD
      UICUBUC $.
      $( [26-Mar-2007] $)
  $}

  $( Swap the arguments of a class difference. $)
  difcom $p |- ( ( A \ B ) C_ C <-> ( A \ C ) C_ B ) $=
    ( cun wss cdif uncom sseq2i ssundif 3bitr3i ) ABCDZEACBDZEABFCEACFBEKLABCGH
    ABCIACBIJ $.
    $( [29-Mar-2007] $)

  $( Distributive law for class difference.  Exercise 4.8 of [Stoll] p. 16. $)
  difdifdir $p |- ( ( A \ B ) \ C ) = ( ( A \ C ) \ ( B \ C ) ) $=
    ( cdif cvv cun cin c0 difdisj incom eqtr3i uneq2i invdif un0 dif23
    3eqtr4ri indi 3eqtr4i indm difeq2i ddif 3eqtr3ri ineq2i 3eqtri ) ABDCDZACDZ
    EBDZCFZGZUFEBCDZDZGUFUJDUFUGGZHFZULUFCGZFUEUIHUNULCUFGHUNCAICUFJKLULUFBDUMU
    EUFBMULNABCOPUFUGCQRUHUKUFEBECDZGZDUGEUODZFUKUHBUOSUPUJEBCMTUQCUGCUALUBUCUF
    UJMUD $.
    $( [18-Aug-2004] $)

  ${
    $d x A $.
    $( Theorem 19.2 of [Margaris] p. 89 with restricted quantifiers (compare
       ~ 19.2 ).  The restricted version is valid only when the domain of
       quantification is not empty. $)
    r19.2z $p |- ( ( A =/= (/) /\ A. x e. A ph ) -> E. x e. A ph ) $=
      ( wral c0 wne wrex cv wcel wex wa wi wal df-ral exintr sylbi n0 df-rex
      3imtr4g impcom ) ABCDZCEFZABCGZUABHCIZBJZUDAKBJZUBUCUAUDALBMUEUFLABCNUDAB
      OPBCQABCRST $.
      $( [9-Dec-2006] $) $( [15-Nov-2003] $)

    $( A response to the notion that the condition ` A =/= (/) ` can be removed
       in ~ r19.2z .  Interestingly enough, ` ph ` does not figure in the
       left-hand side.  (Contributed by Jeff Hankins, 24-Aug-2009.) $)
    r19.2zb $p |- ( A =/= (/) <-> ( A. x e. A ph -> E. x e. A ph ) ) $=
      ( c0 wne wral wrex wi r19.2z ex wceq cv wcel noel pm2.21i rgen raleq
      mpbiri necon3bi wa wex exsimpl df-rex n0 3imtr4i ja impbii ) CDEZABCFZABC
      GZHUHUIUJABCIJUIUJUHUICDCDKUIABDFABDBLZDMAUKNOPABCDQRSUKCMZATBUAULBUAUJUH
      ULABUBABCUCBCUDUEUFUG $.
      $( [18-Jun-2011] $) $( [24-Aug-2009] $)
  $}

  ${
    $d x A $.
    r19.3rz.1 $e |- ( ph -> A. x ph ) $.
    $( Restricted quantification of wff not containing quantified variable. $)
    r19.3rz $p |- ( A =/= (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wne cv wcel wex wi wral wb n0 biimt sylbi wal df-ral 19.23 bitri
      syl6bbr ) CEFZABGCHZBIZAJZABCKZUAUCAUDLBCMUCANOUEUBAJBPUDABCQUBABDRST $.
      $( [26-Jun-2011] $) $( [26-Oct-2010] $)

    $( Restricted quantifier version of Theorem 19.28 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.28z $p |- ( A =/= (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( ph /\ A. x e. A ps ) ) ) $=
      ( c0 wne wral wa r19.3rz anbi1d r19.26 syl6rbbr ) DFGZABCDHZIACDHZOIABICD
      HNAPOACDEJKABCDLM $.
      $( [26-Jun-2011] $) $( [26-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x ph $.
    $( Restricted quantification of wff not containing quantified variable. $)
    r19.3rzv $p |- ( A =/= (/) -> ( ph <-> A. x e. A ph ) ) $=
      ( c0 wne cv wcel wex wi wral wb n0 biimt sylbi wal df-ral 19.23v bitri
      syl6bbr ) CDEZABFCGZBHZAIZABCJZTUBAUCKBCLUBAMNUDUAAIBOUCABCPUAABQRS $.
      $( [5-Oct-2006] $) $( [10-Mar-1997] $)

    $( Restricted quantification of wff not containing quantified variable. $)
    r19.9rzv $p |- ( A =/= (/) -> ( ph <-> E. x e. A ph ) ) $=
      ( c0 wne wn wral wrex r19.3rzv bicomd con2bid dfrex2 syl6bbr ) CDEZAAFZBC
      GZFABCHNPANOPOBCIJKABCLM $.
      $( [10-Oct-2006] $) $( [27-May-1998] $)

    $( Restricted quantifier version of Theorem 19.28 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.28zv $p |- ( A =/= (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( ph /\ A. x e. A ps ) ) ) $=
      ( c0 wne wral wa r19.3rzv anbi1d r19.26 syl6rbbr ) DEFZABCDGZHACDGZNHABHC
      DGMAONACDIJABCDKL $.
      $( [5-Oct-2006] $) $( [19-Aug-2004] $)

    $( Restricted quantifier version of Theorem 19.37 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty.
       (Contributed by Paul Chapman, 8-Oct-2007.) $)
    r19.37zv $p |- ( A =/= (/) ->
                   ( E. x e. A ( ph -> ps ) <-> ( ph -> E. x e. A ps ) ) ) $=
      ( c0 wne wrex wi wral r19.3rzv imbi1d r19.35 syl6rbbr ) DEFZABCDGZHACDIZO
      HABHCDGNAPOACDJKABCDLM $.
      $( [8-Oct-2007] $)

    $( Restricted version of Theorem 19.45 of [Margaris] p. 90. $)
    r19.45zv $p |- ( A =/= (/) ->
                   ( E. x e. A ( ph \/ ps ) <-> ( ph \/ E. x e. A ps ) ) ) $=
      ( c0 wne wrex wo r19.9rzv orbi1d r19.43 syl6rbbr ) DEFZABCDGZHACDGZNHABHC
      DGMAONACDIJABCDKL $.
      $( [10-Oct-2006] $) $( [27-May-1998] $)
  $}

  ${
    $d x A $.
    r19.27z.1 $e |- ( ps -> A. x ps ) $.
    $( Restricted quantifier version of Theorem 19.27 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.27z $p |- ( A =/= (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( A. x e. A ph /\ ps ) ) ) $=
      ( c0 wne wral wa r19.3rz anbi2d r19.26 syl6rbbr ) DFGZACDHZBIOBCDHZIABICD
      HNBPOBCDEJKABCDLM $.
      $( [26-Jun-2011] $) $( [26-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x ps $.
    $( Restricted quantifier version of Theorem 19.27 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.27zv $p |- ( A =/= (/) ->
                   ( A. x e. A ( ph /\ ps ) <-> ( A. x e. A ph /\ ps ) ) ) $=
      ( c0 wne wral wa r19.3rzv anbi2d r19.26 syl6rbbr ) DEFZACDGZBHNBCDGZHABHC
      DGMBONBCDIJABCDKL $.
      $( [12-Oct-2006] $) $( [19-Aug-2004] $)

    $( Restricted quantifier version of Theorem 19.36 of [Margaris] p. 90.  It
       is valid only when the domain of quantification is not empty. $)
    r19.36zv $p |- ( A =/= (/) ->
                   ( E. x e. A ( ph -> ps ) <-> ( A. x e. A ph -> ps ) ) ) $=
      ( c0 wne wral wi wrex r19.9rzv imbi2d r19.35 syl6rbbr ) DEFZACDGZBHOBCDIZ
      HABHCDINBPOBCDJKABCDLM $.
      $( [13-Oct-2006] $) $( [20-Sep-2003] $)
  $}

  ${
    $d x A $.
    $( Vacuous quantification is always true.  (The proof was shortened by
       Andrew Salmon, 26-Jun-2011.) $)
    rzal $p |- ( A = (/) -> A. x e. A ph ) $=
      ( c0 wceq cv wcel ne0i necon2bi pm2.21d r19.21aiv ) CDEZABCLBFZCGZANCDCMH
      IJK $.
      $( [26-Jun-2011] $) $( [11-Mar-1997] $)

    $( Vacuous quantification is always true. $)
    rzalOLD $p |- ( A = (/) -> A. x e. A ph ) $=
      ( c0 wceq cv wcel eleq2 noel pm2.21i syl6bi r19.21aiv ) CDEZABCMBFZCGNDGZ
      ACDNHOANIJKL $.
      $( [11-Mar-1997] $)

    $( Restricted existential quantification implies its restriction is
       nonempty.  (Contributed by Szymon Jaroszewicz, 3-Apr-2007.) $)
    rexn0 $p |- ( E. x e. A ph -> A =/= (/) ) $=
      ( c0 wne cv wcel ne0i a1d r19.23aiv ) ACDEZBCBFZCGKACLHIJ $.
      $( [1-Mar-2007] $)

    $( Idempotent law for restricted quantifier. $)
    ralidm $p |- ( A. x e. A A. x e. A ph <-> A. x e. A ph ) $=
      ( c0 wceq wral wb rzal 2thd wn cv wcel wex neq0 wi biimt wal df-ral
      hbra1 19.23 bitri syl6rbbr sylbi pm2.61i ) CDEZABCFZBCFZUFGZUEUGUFUFBCHAB
      CHIUEJBKCLZBMZUHBCNUJUFUJUFOZUGUJUFPUGUIUFOBQUKUFBCRUIUFBABCSTUAUBUCUD $.
      $( [28-Mar-1997] $)
  $}

  $( Vacuous universal quantification is always true. $)
  ral0 $p |- A. x e. (/) ph $=
    ( c0 cv wcel noel pm2.21i rgen ) ABCBDZCEAIFGH $.
    $( [20-Oct-2005] $)

  ${
    $d x A $.
    ralf0.1 $e |- -. ph $.
    $( The quantification of a falsehood is vacuous when true. $)
    ralf0 $p |- ( A. x e. A ph <-> A = (/) ) $=
      ( wral c0 wceq cv wcel wi wal wn con3 mpi alimi df-ral eq0 3imtr4i rzal
      impbii ) ABCEZCFGZBHCIZAJZBKUCLZBKUAUBUDUEBUDALUEDUCAMNOABCPBCQRABCST $.
      $( [26-Nov-2005] $)
  $}

  $( MAY BE REVISED - shorten r19.3zv, r19.27zv, r19.28zv, raaanv w/ non-v $)
  ${
    $d x y A $.
    raaan.1 $e |- ( ph -> A. y ph ) $.
    raaan.2 $e |- ( ps -> A. x ps ) $.
    $( Rearrange restricted quantifiers. $)
    raaan $p |- ( A. x e. A A. y e. A ( ph /\ ps ) <->
                  ( A. x e. A ph /\ A. y e. A ps ) ) $=
      ( wa wral wb c0 wceq rzal pm5.1 syl12anc wne r19.28z ralbidv cv wcel
      ax-17 hbral r19.27z bitrd pm2.61ine ) ABHDEIZCEIZACEIZBDEIZHZJZEKEKLUGUHU
      IUKUFCEMACEMBDEMUGUJNOEKPZUGAUIHZCEIUJULUFUMCEABDEFQRAUICEBCDEDSETCUAGUBU
      CUDUE $.
      $( [26-Jun-2011] $) $( [26-Oct-2010] $)
  $}

  ${
    $d y ph $.  $d x ps $.  $d x y A $.
    $( Rearrange restricted quantifiers. $)
    raaanv $p |- ( A. x e. A A. y e. A ( ph /\ ps ) <->
                  ( A. x e. A ph /\ A. y e. A ps ) ) $=
      ( wa wral wb c0 wceq rzal pm5.1 syl12anc wne r19.28zv ralbidv r19.27zv
      bitrd pm2.61ine ) ABFDEGZCEGZACEGZBDEGZFZHZEIEIJUAUBUCUETCEKACEKBDEKUAUDL
      MEINZUAAUCFZCEGUDUFTUGCEABDEOPAUCCEQRS $.
      $( [11-Mar-1997] $)
  $}

  ${
    $d x A w $.  $d v x $.  $d v A $.  $d w v $.  $d w y $.
    $( Lemma for ~ sbss .  (The proof was shortened by Andrew Salmon,
       29-Jun-2011.) $)
    sbsslem $p |- ( [ y / x ] x C_ A
                  <-> A. w ( ( w e. y /\ w e. A ) <-> w e. y ) ) $=
      ( cv wss wsbc wcel wi wal wa wb dfss2 sbbii sbal sbim elsb4 ax-17 sbf
      imbi12i simpl ancl impbid2 bi2 simpr syl6 impbii 3bitri albii ) AEZDFZABE
      ZGCEZUJHZUMDHZIZCJZAULGUPAULGZCJUMULHZUOKZUSLZCJUKUQABCUJDMNUPCABOURVACUR
      UNAULGZUOAULGZIUSUOIZVAUNUOABPVBUSVCUOBACQUOABUOARSTVDVAVDUTUSUSUOUAUSUOU
      BUCVAUSUTUOUTUSUDUSUOUEUFUGUHUIUH $.
      $( [29-Jun-2011] $) $( [7-Jul-2010] $)

    $( Lemma for ~ sbss . $)
    sbsslemOLD $p |- ( [ y / x ] x C_ A
                  <-> A. w ( ( w e. y /\ w e. A ) <-> w e. y ) ) $=
      ( vv cv wss wsbc wcel wa cab wceq wb wal cin df-ss df-in eqeq1i bitri
      sbbii dfcleq df-clab sban elsb3 clelsb3 anbi12i 3bitri bibi1i albii sbal
      sbbi elsb4 ax-17 sbf bibi12i ) AFZDGZABFZHEFZUPIZUSDIZJZEKZUPLZAURHCFZUPI
      ZVEDIZJZVFMZCNZAURHZVEURIZVGJZVLMZCNZUQVDABUQUPDOZUPLVDUPDPVPVCUPEUPDQRST
      VDVJABVDVEVCIZVFMZCNVJCVCUPUAVRVICVQVHVFVQVBEVEHUTEVEHZVAEVEHZJVHVBCEUBUT
      VAECUCVSVFVTVGCEAUDCEDUEUFUGUHUISTVKVIAURHZCNVOVICABUJWAVNCWAVHAURHZVFAUR
      HZMVNVHVFABUKWBVMWCVLWBWCVGAURHZJVMVFVGABUCWCVLWDVGBACULZVGABVGAUMUNUFSWE
      UOSUISUG $.
      $( [7-Jul-2010] $)
  $}

  ${
    $d z y $.  $d z A x $.
    $( Set substitution into the first argument of a subset relation.
       (Contributed by Rodolfo Medina, 7-Jul-2010.)  (Unnecessary distinct
       variable restrictions were removed by Andrew Salmon, 26-Jun-2011.) $)
    sbss $p |- ( [ y / x ] x C_ A <-> y C_ A ) $=
      ( vz cv wss wsbc ax-17 sseq1 sbie sbbii sbco2 3bitr3i ) AEZCFZADEZGZDBEZG
      PCFZDRGOARGRCFZQSDBOSADSAHNPCIJKOABDODHLSTDBTDHPRCIJM $.
      $( [26-Jun-2011] $) $( [7-Jul-2010] $)
  $}

  ${
    $d x v A $.  $d v y A $.
    $( Set substitution into the first argument of a subset relation.
       (Contributed by Rodolfo Medina, 7-Jul-2010.) $)
    sbssOLD $p |- ( [ y / x ] x C_ A <-> y C_ A ) $=
      ( vv cv wss wsbc wcel wa wb wal sbsslem sbid 3bitr2i ) AECFABEZGDEZOHZPCH
      IQJDKOCFZBOGRABDCLBBDCLRBMN $.
      $( [7-Jul-2010] $)
  $}

