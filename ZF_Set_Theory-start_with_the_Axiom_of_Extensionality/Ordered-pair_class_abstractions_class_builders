$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ordered-pair class abstractions (class builders)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend class notation to include ordered-pair class abstraction (class
     builder). $)
  copab $a class { <. x , y >. | ph } $.

  ${
    $d x z $.  $d y z $.  $d z ph $.
    $( Define the class abstraction of a collection of ordered pairs.
       Definition 3.3 of [Monk1] p. 34.  Usually ` x ` and ` y ` are distinct,
       although the definition doesn't strictly require it (see ~ dfid2 for a
       case where they are not distinct).  The brace notation is called "class
       abstraction" by Quine; it is also (more commonly) called a "class
       builder" in the literature.  An alternate definition using no
       existential quantifiers is shown by ~ dfopab2 . $)
    df-opab $a |- { <. x , y >. | ph } =
                  { z | E. x E. y ( z = <. x , y >. /\ ph ) } $.
  $}

  ${
    $d x z R $.  $d y z R $.
    $( The collection of ordered pairs in a class is a subclass of it.  (The
       proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    opabss $p |- { <. x , y >. | x R y } C_ R $=
      ( vz cv wbr copab cop wceq wa wex cab df-opab wcel eleq1 biimpar df-br
      sylan2b 19.23aivv abssi eqsstri ) AEZBEZCFZABGDEZUBUCHZIZUDJZBKAKZDLCUDAB
      DMUIDCUHUECNZABUGUFCNZUJUDUGUJUKUEUFCOPUBUCCQRSTUA $.
      $( [9-Jul-2011] $) $( [27-Dec-1996] $)

    $( The collection of ordered pairs in a class is a subclass of it. $)
    opabssOLD $p |- { <. x , y >. | x R y } C_ R $=
      ( vz cv wbr copab wcel cab cop wceq wa wex df-opab eleq1 biimpar df-br
      sylan2b 19.23aivv ss2abi eqsstri abid2 sseqtri ) AEZBEZCFZABGZDEZCHZDIZCU
      GUHUDUEJZKZUFLZBMAMZDIUJUFABDNUNUIDUMUIABULUKCHZUIUFULUIUOUHUKCOPUDUECQRS
      TUADCUBUC $.
      $( [27-Dec-1996] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z ph $.  $d z ps $.  $d z ch $.
    opabbid.1 $e |- ( ph -> A. x ph ) $.
    opabbid.2 $e |- ( ph -> A. y ph ) $.
    opabbid.3 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal ordered-pair class abstractions (deduction
       rule).  (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    opabbid $p |- ( ph -> { <. x , y >. | ps } = { <. x , y >. | ch } ) $=
      ( vz cv cop wceq wa wex cab copab anbi2d exbid abbidv df-opab 3eqtr4g )
      AIJDJEJKLZBMZENZDNZIOUBCMZENZDNZIOBDEPCDEPAUEUHIAUDUGDFAUCUFEGABCUBHQRRSB
      DEITCDEITUA $.
      $( [9-Jul-2011] $) $( [21-Feb-2004] $)

    $( Equivalent wff's yield equal ordered-pair class abstractions (deduction
       rule). $)
    opabbidOLD $p |- ( ph -> { <. x , y >. | ps } = { <. x , y >. | ch } ) $=
      ( vz cv cop wceq wa wex cab copab ax-17 anbi2d exbid abbid df-opab
      3eqtr4g ) AIJDJEJKLZBMZENZDNZIOUCCMZENZDNZIOBDEPCDEPAUFUIIAIQAUEUHDFAUDUG
      EGABCUCHRSSTBDEIUACDEIUAUB $.
      $( [24-Feb-2004] $) $( [21-Feb-2004] $)
  $}

  ${
    $d x ph $.  $d y z ph $.  $d z ps $.  $d z ch $.
    opabbidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal ordered-pair class abstractions (deduction
       rule). $)
    opabbidv $p |- ( ph -> { <. x , y >. | ps } = { <. x , y >. | ch } ) $=
      ( ax-17 opabbid ) ABCDEADGAEGFH $.
      $( [15-May-1995] $)
  $}

  ${
    $d x z $.  $d y z $.  $d z ph $.  $d z ps $.
    opabbii.1 $e |- ( ph <-> ps ) $.
    $( Equivalent wff's yield equal class abstractions. $)
    opabbii $p |- { <. x , y >. | ph } = { <. x , y >. | ps } $=
      ( vz cv wceq copab eqid wb a1i opabbidv ax-mp ) FGZOHZACDIBCDIHOJPABCDABK
      PELMN $.
      $( [25-Jul-2011] $) $( [15-May-1995] $)
  $}

  ${
    $d x y z w v $.  $d v ph $.  $d v ps $.
    cbvopab.1 $e |- ( ph -> A. z ph ) $.
    cbvopab.2 $e |- ( ph -> A. w ph ) $.
    cbvopab.3 $e |- ( ps -> A. x ps ) $.
    cbvopab.4 $e |- ( ps -> A. y ps ) $.
    cbvopab.5 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables in an ordered-pair class
       abstraction, using implicit substitution. $)
    cbvopab $p |- { <. x , y >. | ph } = { <. z , w >. | ps } $=
      ( vv cv cop wceq wa wex cab copab ax-17 hban opeq12 eqeq2d anbi12d
      cbvex2 abbii df-opab 3eqtr4i ) LMZCMZDMZNZOZAPZDQCQZLRUIEMZFMZNZOZBPZFQEQ
      ZLRACDSBEFSUOVALUNUTCDEFUMAEUMETGUAUMAFUMFTHUAUSBCUSCTIUAUSBDUSDTJUAUJUPO
      UKUQOPZUMUSABVBULURUIUJUKUPUQUBUCKUDUEUFACDLUGBEFLUGUH $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d x y z w $.  $d z w v ph $.  $d x y v ps $.
    cbvopabv.1 $e |- ( ( x = z /\ y = w ) -> ( ph <-> ps ) ) $.
    $( Rule used to change bound variables in an ordered-pair class
       abstraction, using implicit substitution. $)
    cbvopabv $p |- { <. x , y >. | ph } = { <. z , w >. | ps } $=
      ( ax-17 cbvopab ) ABCDEFAEHAFHBCHBDHGI $.
      $( [15-Oct-1996] $)
  $}

  ${
    $d x y z w $.  $d w ph $.  $d w ps $.
    cbvopab1.1 $e |- ( ph -> A. z ph ) $.
    cbvopab1.2 $e |- ( ps -> A. x ps ) $.
    cbvopab1.3 $e |- ( x = z -> ( ph <-> ps ) ) $.
    $( Change first bound variable in an ordered-pair class abstraction, using
       explicit substitution. $)
    cbvopab1 $p |- { <. x , y >. | ph } = { <. z , y >. | ps } $=
      ( vw cv cop wceq wa wex cab copab ax-17 hban hbex opeq1 eqeq2d anbi12d
      exbidv cbvex abbii df-opab 3eqtr4i ) IJZCJZDJZKZLZAMZDNZCNZIOUHEJZUJKZLZB
      MZDNZENZIOACDPBEDPUOVAIUNUTCEUMEDULAEULEQFRSUSCDURBCURCQGRSUIUPLZUMUSDVBU
      LURABVBUKUQUHUIUPUJTUAHUBUCUDUEACDIUFBEDIUFUG $.
      $( [17-Oct-2004] $) $( [6-Oct-2004] $)
  $}

  ${
    $d x y z w $.  $d z w ph $.
    $( Change first bound variable in an ordered-pair class abstraction, using
       explicit substitution. $)
    cbvopab1s $p |- { <. x , y >. | ph } = { <. z , y >. | [ z / x ] ph } $=
      ( vw cv cop wceq wa wex cab wsbc copab ax-17 hbs1 hban hbex opeq1 eqeq2d
      sbequ12 anbi12d exbidv cbvex abbii df-opab 3eqtr4i ) EFZBFZCFZGZHZAIZCJZB
      JZEKUGDFZUIGZHZABUOLZIZCJZDJZEKABCMURDCMUNVAEUMUTBDUMDNUSBCUQURBUQBNABDOP
      QUHUOHZULUSCVBUKUQAURVBUJUPUGUHUOUIRSABDTUAUBUCUDABCEUEURDCEUEUF $.
      $( [5-Nov-2004] $) $( [31-Jul-2003] $)
  $}

  ${
    $d x y z $.  $d z ph $.  $d x ps $.
    cbvopab1v.1 $e |- ( x = z -> ( ph <-> ps ) ) $.
    $( Rule used to change the first bound variable in an ordered pair
       abstraction, using implicit substitution.  (The proof was shortened by
       Eric Schmidt, 4-Apr-2007.) $)
    cbvopab1v $p |- { <. x , y >. | ph } = { <. z , y >. | ps } $=
      ( ax-17 cbvopab1 ) ABCDEAEGBCGFH $.
      $( [4-Apr-2007] $) $( [31-Jul-2003] $)
  $}

  ${
    $d x y z w $.  $d z w ph $.  $d y w ps $.
    cbvopab2v.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Rule used to change the second bound variable in an ordered pair
       abstraction, using implicit substitution. $)
    cbvopab2v $p |- { <. x , y >. | ph } = { <. x , z >. | ps } $=
      ( vw cv cop wceq wa wex cab copab opeq2 eqeq2d anbi12d cbvexv exbii
      abbii df-opab 3eqtr4i ) GHZCHZDHZIZJZAKZDLZCLZGMUCUDEHZIZJZBKZELZCLZGMACD
      NBCENUJUPGUIUOCUHUNDEUEUKJZUGUMABUQUFULUCUEUKUDOPFQRSTACDGUABCEGUAUB $.
      $( [2-Sep-1999] $)
  $}

  ${
    $d w y z A $.  $d w ph $.  $d w x y z $.
    $( Move substitution into a class abstraction. $)
    csbopabg $p |- ( A e. B -> [_ A / x ]_ { <. y , z >. | ph } =
               { <. y , z >. | [ A / x ] ph } ) $=
      ( vw wcel cvv copab csb wsbc wceq elisset cv cop wa wex cab df-opab
      csbeq2i csbabg sbcexg sbcang ax-17 sbcgf anbi1d bitrd exbidv abbidv
      eqtrd eqcomi a1i 3eqtrd syl ) EFHEIHZBEACDJZKZABELZCDJZMEFNUPURBEGOCODOPM
      ZAQZDRZCRZGSZKZVAUSQZDRZCRZGSZUTBEUQVEIACDGTUAUPVFVDBELZGSVJVDBGEIUBUPVKV
      IGUPVKVCBELZCRVIVCCBEIUCUPVLVHCUPVLVBBELZDRVHVBDBEIUCUPVMVGDUPVMVABELZUSQ
      VGVAABEIUDUPVNVAUSVABEIVABUEUFUGUHUIUHUIUHUJUKVJUTMUPUTVJUSCDGTULUMUNUO
      $.
      $( [12-Aug-2007] $) $( [6-Aug-2007] $)
  $}

  ${
    $d x z $.  $d y z $.  $d ph z $.  $d ps z $.
    $( Union of two ordered pair class abstractions. $)
    unopab $p |- ( { <. x , y >. | ph } u. { <. x , y >. | ps } ) =
               { <. x , y >. | ( ph \/ ps ) } $=
      ( vz cv cop wceq wa wex cab cun wo copab unab 19.43 andi exbii bitr2i
      bitr3i abbii eqtri df-opab uneq12i 3eqtr4i ) EFCFDFGHZAIZDJZCJZEKZUFBIZDJ
      ZCJZEKZLZUFABMZIZDJZCJZEKZACDNZBCDNZLUPCDNUOUIUMMZEKUTUIUMEOVCUSEVCUHULMZ
      CJUSUHULCPVDURCURUGUKMZDJVDUQVEDUFABQRUGUKDPSRTUAUBVAUJVBUNACDEUCBCDEUCUD
      UPCDEUCUE $.
      $( [30-Sep-2002] $)
  $}

