$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Indexed union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c U_ $. $( Underlined big cup. $)
  $c |^|_ $. $( Underlined big cap. $)

  $( Extend class notation to include indexed union.  Note:  Historically
     (prior to 21-Oct-2005), set.mm used the notation ` U. x e. A B ` , with
     the same union symbol as ~ cuni .  While that syntax was unambiguous, it
     did not allow for LALR parsing of the syntax constructions in set.mm.  The
     new syntax uses as distinguished symbol ` U_ ` instead of ` U. ` and does
     allow LALR parsing.  Thanks to Peter Backes for suggesting this change. $)
  ciun $a class U_ x e. A B $.

  $( Extend class notation to include indexed intersection.  Note:
     Historically (prior to 21-Oct-2005), set.mm used the notation
     ` |^| x e. A B ` , with the same intersection symbol as ~ cint .  Although
     that syntax was unambiguous, it did not allow for LALR parsing of the
     syntax constructions in set.mm.  The new syntax uses a distinguished
     symbol ` |^|_ ` instead of ` |^| ` and does allow LALR parsing.  Thanks to
     Peter Backes for suggesting this change. $)
  ciin $a class |^|_ x e. A B $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define indexed union.  Definition of [Stoll] p. 45.  In normal use,
       ` A ` is independent of ` x ` , and ` B ` depends on ` x ` i.e. can be
       read informally as ` B ( x ) ` .  We call ` x ` the index, ` A ` the
       index set, and ` B ` the indexed set.  In most books, ` x e. A ` is
       written as a subscript or underneath a union symbol ` U. ` .  We use a
       special union symbol ` U_ ` to make it easier to distinguish from plain
       class union.  In many theorems, you will see that ` x ` and ` A ` are in
       the same distinct variable group (meaning ` A ` cannot depend on ` x ` )
       and that ` B ` and ` x ` do not share a distinct variable group (meaning
       that can be thought of as ` B ( x ) ` i.e. can be substituted with a
       class expression containing ` x ` ).  An alternate definition tying
       indexed union to ordinary union is ~ dfiun2 .  Theorem ~ uniiun provides
       a definition of ordinary union in terms of indexed union.  Theorems
       ~ fniunfv and ~ funiunfv are useful when ` B ` is a function. $)
    df-iun $a |- U_ x e. A B = { y | E. x e. A y e. B } $.

    $( Define indexed intersection.  Definition of [Stoll] p. 45.  See the
       remarks for its sibling operation of indexed union ~ df-iun .  An
       alternate definition tying indexed intersection to ordinary intersection
       is ~ dfiin2 .  Theorem ~ intiin provides a definition of ordinary
       intersection in terms of indexed intersection. $)
    df-iin $a |- |^|_ x e. A B = { y | A. x e. A y e. B } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.
    $( Membership in indexed union. $)
    eliun $p |- ( A e. U_ x e. B C <-> E. x e. B A e. C ) $=
      ( vy ciun wcel cvv wrex elisset r19.23aivw cv wceq eleq1 rexbidv df-iun
      elab2g pm5.21nii ) BACDFZGBHGZBDGZACIZBSJUATACBDJKELZDGZACIUBEBSHUCBMUDUA
      ACUCBDNOAECDPQR $.
      $( [3-Sep-2003] $)

    $( Membership in indexed intersection. $)
    eliin $p |- ( A e. V -> ( A e. |^|_ x e. B C <-> A. x e. B A e. C ) ) $=
      ( vy cv wcel wral ciin wceq eleq1 ralbidv df-iin elab2g ) FGZDHZACIBDHZAC
      IFBACDJEPBKQRACPBDLMAFCDNO $.
      $( [3-Sep-2003] $)
  $}

  ${
    $d y z A $.  $d x z B $.  $d z C $.  $d x y $.
    $( Commutation of indexed unions. $)
    iuncom $p |- U_ x e. A U_ y e. B C = U_ y e. B U_ x e. A C $=
      ( vz ciun cv wcel wrex rexcom eliun rexbii 3bitr4i eqriv ) FACBDEGZGZBDAC
      EGZGZFHZPIZACJZTRIZBDJZTQITSITEIZBDJZACJUEACJZBDJUBUDUEABCDKUAUFACBTDELMU
      CUGBDATCELMNATCPLBTDRLNO $.
      $( [18-Dec-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Indexed union of a constant class, i.e. where ` B ` does not depend on
       ` x ` .  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    iunconst $p |- ( A =/= (/) -> U_ x e. A B = B ) $=
      ( vy c0 wne ciun cv wcel wrex r19.9rzv eliun syl6rbbr eqrdv ) BEFZDABCGZC
      ODHZCIZRABJQPIRABKAQBCLMN $.
      $( [25-Jul-2011] $) $( [5-Sep-2004] $)
  $}

  ${
    $d x y $.  $d y z A $.  $d x z B $.  $d z C $.
    $( Law combining indexed union with indexed intersection.  Eq.  (14),
       p. 109, in _Set Theory, with an Introduction to Descriptive Set Theory_
       by K. Kuratowski and A. Mostowski (2nd ed., 1976).  (This theorem also
       appears as the last example at
       ~ http://en.wikipedia.org/wiki/Union%5F%28set%5Ftheory%29 .)  (The proof
       was shortened by Andrew Salmon, 25-Jul-2011.) $)
    iuniin $p |- U_ x e. A |^|_ y e. B C C_ |^|_ y e. B U_ x e. A C $=
      ( vz ciin ciun cv wcel wrex wral r19.12 cvv wb visset eliin ax-mp rexbii
      eliun ralbii 3imtr4i ssriv ) FACBDEGZHZBDACEHZGZFIZUDJZACKZUHUFJZBDLZUHUE
      JUHUGJZUHEJZBDLZACKUNACKZBDLUJULUNABCDMUIUOACUHNJZUIUOOFPZBUHDENQRSUKUPBD
      AUHCETUAUBAUHCUDTUQUMULOURBUHDUFNQRUBUC $.
      $( [25-Jul-2011] $) $( [17-Aug-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Subclass theorem for indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunss1 $p |- ( A C_ B -> U_ x e. A C C_ U_ x e. B C ) $=
      ( vy wss ciun cv wcel wrex ssrexv eliun 3imtr4g ssrdv ) BCFZEABDGZACDGZOE
      HZDIZABJSACJRPIRQISABCKARBDLARCDLMN $.
      $( [25-Jul-2011] $) $( [10-Dec-2004] $)

    $( Subclass theorem for indexed union. $)
    iinss1 $p |- ( A C_ B -> |^|_ x e. B C C_ |^|_ x e. A C ) $=
      ( vy wss ciin cv wcel wral ssralv cvv wb visset eliin ax-mp 3imtr4g
      ssrdv ) BCFZEACDGZABDGZSEHZDIZACJZUCABJZUBTIZUBUAIZUCABCKUBLIZUFUDMENZAUB
      CDLOPUHUGUEMUIAUBBDLOPQR $.
      $( [24-Jan-2012] $)

    $( Equality theorem for indexed union. $)
    iuneq1 $p |- ( A = B -> U_ x e. A C = U_ x e. B C ) $=
      ( wss wa ciun wceq iunss1 anim12i eqss 3imtr4i ) BCEZCBEZFABDGZACDGZEZPOE
      ZFBCHOPHMQNRABCDIACBDIJBCKOPKL $.
      $( [27-Jun-1998] $)

    $( Equality theorem for restricted existential quantifier. $)
    iineq1 $p |- ( A = B -> |^|_ x e. A C = |^|_ x e. B C ) $=
      ( vy wceq cv wcel wral cab ciin raleq abbidv df-iin 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [27-Jun-1998] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Subclass theorem for indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    ss2iun $p |- ( A. x e. A B C_ C -> U_ x e. A B C_ U_ x e. A C ) $=
      ( vy wss wral ciun cv wcel wrex wi ssel ralimi rexim syl eliun 3imtr4g
      ssrdv ) CDFZABGZEABCHZABDHZUAEIZCJZABKZUDDJZABKZUDUBJUDUCJUAUEUGLZABGUFUH
      LTUIABCDUDMNUEUGABOPAUDBCQAUDBDQRS $.
      $( [25-Jul-2011] $) $( [26-Nov-2003] $)

    $( Equality theorem for indexed union. $)
    iuneq2 $p |- ( A. x e. A B = C -> U_ x e. A B = U_ x e. A C ) $=
      ( wss wral wa ciun wceq ss2iun anim12i eqss ralbii r19.26 bitri 3imtr4i
      ) CDEZABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQS
      GZABFUAUFUHABCDLMQSABNOUBUCLP $.
      $( [22-Oct-2003] $)

    $( Equality theorem for indexed intersection.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    iineq2 $p |- ( A. x e. A B = C -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( vy wceq wral cv wcel cab ciin wb eleq2 ralimi ralbi syl abbidv df-iin
      3eqtr4g ) CDFZABGZEHZCIZABGZEJUBDIZABGZEJABCKABDKUAUDUFEUAUCUELZABGUDUFLT
      UGABCDUBMNUCUEABOPQAEBCRAEBDRS $.
      $( [25-Jul-2011] $) $( [22-Oct-2003] $)
  $}

  ${
    iuneq2i.1 $e |- ( x e. A -> B = C ) $.
    $( Equality inference for indexed union. $)
    iuneq2i $p |- U_ x e. A B = U_ x e. A C $=
      ( wceq ciun iuneq2 mprg ) CDFABCGABDGFABABCDHEI $.
      $( [22-Oct-2003] $)

    $( Equality inference for indexed intersection. $)
    iineq2i $p |- |^|_ x e. A B = |^|_ x e. A C $=
      ( wceq ciin iineq2 mprg ) CDFABCGABDGFABABCDHEI $.
      $( [22-Oct-2003] $)
  $}

  ${
    iineq2d.1 $e |- ( ph -> A. x ph ) $.
    iineq2d.2 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Equality deduction for indexed intersection. $)
    iineq2d $p |- ( ph -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( wceq wral ciin cv wcel ex r19.21ai iineq2 syl ) ADEHZBCIBCDJBCEJHAQBCFA
      BKCLQGMNBCDEOP $.
      $( [7-Dec-2011] $)
  $}

  ${
    $d x ph $.
    iuneq2dv.1 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Equality deduction for indexed union. $)
    iuneq2dv $p |- ( ph -> U_ x e. A B = U_ x e. A C ) $=
      ( wceq wral ciun r19.21aiva iuneq2 syl ) ADEGZBCHBCDIBCEIGAMBCFJBCDEKL $.
      $( [5-Oct-2006] $) $( [3-Aug-2004] $)

    $( Equality deduction for indexed intersection. $)
    iineq2dv $p |- ( ph -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( ax-17 iineq2d ) ABCDEABGFH $.
      $( [3-Jan-2007] $) $( [3-Aug-2004] $)
  $}

  ${
    $d z A $.  $d z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed union. $)
    hbiu1 $p |- ( y e. U_ x e. A B -> A. x y e. U_ x e. A B ) $=
      ( vz ciun cv wcel wrex cab df-iun hbre1 hbab hbxfreq ) ABACDFEGDHZACIZEJA
      ECDKPAEBOACLMN $.
      $( [12-Oct-2003] $)
  $}

  ${
    $d z A $.  $d z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed intersection. $)
    hbii1 $p |- ( y e. |^|_ x e. A B -> A. x y e. |^|_ x e. A B ) $=
      ( vz ciin cv wcel wral cab df-iin hbra1 hbab hbxfreq ) ABACDFEGDHZACIZEJA
      ECDKPAEBOACLMN $.
      $( [15-Oct-2003] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w C z $.  $d w x y z $.
    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    dfiun2g $p |- ( A. x e. A B e. C ->
                  U_ x e. A B = U. { y | E. x e. A y = B } ) $=
      ( vz wcel wral ciun cv wceq wrex cab cuni wa wex hbra1 wb ra4 clel3g
      syl6 imp rexbida rexcom4 syl6bb r19.41v exbii exancom bitri eliun
      eluniab 3bitr4g eqrdv ) DEGZACHZFACDIZBJZDKZACLZBMNZUOFJZDGZACLZVAUQGZUSO
      BPZVAUPGVAUTGUOVCURVDOZACLZBPZVEUOVCVFBPZACLVHUOVBVIACUNACQUOAJCGZVBVIRZU
      OVJUNVKUNACSBVADETUAUBUCVFABCUDUEVHUSVDOZBPVEVGVLBURVDACUFUGUSVDBUHUIUEAV
      ACDUJUSBVAUKULUM $.
      $( [25-Jul-2011] $) $( [23-Mar-2006] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.
    dfiun2.1 $e |- B e. _V $.
    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44.  (Unnecessary distinct variable restrictions
       were removed by David Abernethy, 19-Jun-2012.) $)
    dfiun2 $p |- U_ x e. A B = U. { y | E. x e. A y = B } $=
      ( cvv wcel ciun cv wceq wrex cab cuni dfiun2g a1i mprg ) DFGZACDHBIDJACKB
      LMJACABCDFNQAICGEOP $.
      $( [19-Jun-2012] $) $( [27-Jun-1998] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w C $.  $d w x y z $.
    $( Alternate definition of indexed intersection when ` B ` is a set.
       (Contributed by Jeff Hankins, 27-Aug-2009.) $)
    dfiin2g $p |- ( A. x e. A B e. C
               -> |^|_ x e. A B = |^| { y | E. x e. A y = B } ) $=
      ( vw vz wcel wral cv cab wceq wrex wi wal ciin cint df-ral wb eleq2
      biimprcd 19.21aiv eqid eqeq1 imbi12d cla4gv mpii impbid2 imim2i pm5.74d
      alimi albi syl sylbi albii alcom 19.21v 3bitr2ri r19.23v visset rexbidv
      elab imbi1i bitr4i bitri syl6bb syl5bb abbidv df-iin df-int 3eqtr4g ) DEH
      ZACIZFJZDHZACIZFKGJZBJZDLZACMZBKZHZVNVQHZNZGOZFKACDPWAQVMVPWEFVPAJCHZVONZ
      AOZVMWEVOACRVMWHWFVQDLZWCNZGOZNZAOZWEVMWFVLNZAOZWHWMSZVLACRWOWGWLSZAOWPWN
      WQAWNWFVOWKVLVOWKSWFVLVOWKVOWJGWIWCVOVQDVNTZUAUBVLWKDDLZVODUCWJWSVONGDEWI
      WIWSWCVOVQDDUDWRUEUFUGUHUIUJUKWGWLAULUMUNWMWJACIZGOZWEXAWFWJNZAOZGOXBGOZA
      OWMWTXCGWJACRUOXBAGUPXDWLAWFWJGUQUOURWTWDGWTWIACMZWCNWDWIWCACUSWBXEWCVTXE
      BVQGUTVRVQLVSWIACVRVQDUDVAVBVCVDUOVEVFVGVHAFCDVIFGWAVJVK $.
      $( [20-Sep-2011] $) $( [27-Aug-2009] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w x y z $.
    dfiin2.1 $e |- B e. _V $.
    $( Alternate definition of indexed intersection when ` B ` is a set.
       Definition 15(b) of [Suppes] p. 44.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    dfiin2 $p |- |^|_ x e. A B = |^| { y | E. x e. A y = B } $=
      ( vz ciin cv wceq wrex cab cint wcel wral wi wal clel4 ralbii ralcom4
      r19.23v albii 3bitri cvv wb visset eliin ax-mp elintab 3bitr4i eqriv ) FA
      CDGZBHZDIZACJZBKLZFHZDMZACNZUNUPULMZOZBPZUPUKMZUPUOMURUMUSOZBPZACNVCACNZB
      PVAUQVDACBUPDEQRVCABCSVEUTBUMUSACTUAUBUPUCMVBURUDFUEZAUPCDUCUFUGUNBUPVFUH
      UIUJ $.
      $( [25-Jul-2011] $) $( [28-Jun-1998] $)
  $}

  ${
    $d w y A $.  $d w x A $.  $d w z B $.  $d w z C $.
    cbviun.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbviun.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbviun.3 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       25-Jul-2011.) $)
    cbviun $p |- U_ x e. A B = U_ y e. A C $=
      ( vw ciun cv wcel wrex hblem wceq eleq2d cbvrex eliun 3bitr4i eqriv ) JAD
      EKZBDFKZJLZEMZADNUDFMZBDNUDUBMUDUCMUEUFABDBCJEGOACJFHOALBLPEFUDIQRAUDDESB
      UDDFSTUA $.
      $( [25-Jul-2011] $) $( [26-Mar-2006] $)

    $( Change bound variables in an indexed intersection.  (Contributed by Jeff
       Hankins, 26-Aug-2009.) $)
    cbviin $p |- |^|_ x e. A B = |^|_ y e. A C $=
      ( vw ciin cv wcel wral hblem wceq eleq2d cbvral cvv wb visset eliin
      ax-mp 3bitr4i eqriv ) JADEKZBDFKZJLZEMZADNZUHFMZBDNZUHUFMZUHUGMZUIUKABDBC
      JEGOACJFHOALBLPEFUHIQRUHSMZUMUJTJUAZAUHDESUBUCUOUNULTUPBUHDFSUBUCUDUE $.
      $( [26-Aug-2009] $)
  $}

  ${
    $d x A $.  $d y A $.  $d y z B $.  $d x z C $.
    cbviunv.1 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis. $)
    cbviunv $p |- U_ x e. A B = U_ y e. A C $=
      ( vz cv wcel ax-17 cbviun ) ABGCDEGHZDIBJLEIAJFK $.
      $( [25-Jul-2011] $) $( [15-Sep-2003] $)

    $( Change bound variables in an indexed intersection.  (Contributed by Jeff
       Hankins, 26-Aug-2009.) $)
    cbviinv $p |- |^|_ x e. A B = |^|_ y e. A C $=
      ( vz cv wcel ax-17 cbviin ) ABGCDEGHZDIBJLEIAJFK $.
      $( [26-Aug-2009] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunss $p |- ( U_ x e. A B C_ C <-> A. x e. A B C_ C ) $=
      ( vy ciun wss cv wcel wrex cab wi wal wral df-iun sseq1i abss dfss2
      ralbii ralcom4 r19.23v albii 3bitrri 3bitri ) ABCFZDGEHZCIZABJZEKZDGUHUFD
      IZLZEMZCDGZABNZUEUIDAEBCOPUHEDQUNUGUJLZEMZABNUOABNZEMULUMUPABECDRSUOAEBTU
      QUKEUGUJABUAUBUCUD $.
      $( [25-Jul-2011] $) $( [13-Sep-2003] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed union.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    ssiun $p |- ( E. x e. A C C_ B -> C C_ U_ x e. A B ) $=
      ( vy wss wrex ciun cv wcel wi ssel reximi r19.37av syl eliun syl6ibr
      ssrdv ) DCFZABGZEDABCHZTEIZDJZUBCJZABGZUBUAJTUCUDKZABGUCUEKSUFABDCUBLMUCU
      DABNOAUBBCPQR $.
      $( [25-Jul-2011] $) $( [3-Sep-2003] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Identity law for subset of an indexed union.  (The proof was shortened
       by Andrew Salmon, 25-Jul-2011.) $)
    ssiun2 $p |- ( x e. A -> B C_ U_ x e. A B ) $=
      ( vy cv wcel ciun wrex ra4e ex eliun syl6ibr ssrdv ) AEBFZDCABCGZNDEZCFZQ
      ABHZPOFNQRQABIJAPBCKLM $.
      $( [25-Jul-2011] $) $( [12-Oct-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    ssiun2s.1 $e |- ( x = C -> B = D ) $.
    $( Subset relationship for an indexed union. $)
    ssiun2s $p |- ( C e. A -> D C_ U_ x e. A B ) $=
      ( vy wcel ciun wss cv wi ax-17 hbiu1 hbss hbim wceq eleq1 sseq1d imbi12d
      ssiun2 vtoclgf pm2.43i ) DBHZEABCIZJZAKZBHZCUEJZLUDUFLAGDBGKZDHAMUDUFAUDA
      MAGEUEUJEHAMAGBCNOPUGDQZUHUDUIUFUGDBRUKCEUEFSTABCUAUBUC $.
      $( [26-Oct-2003] $)
  $}

  ${
    $d x y $.  $d x B $.  $d y C $.  $d x D $.
    $( A subclass condition on the members of two indexed classes ` C ( x ) `
       and ` D ( y ) ` that implies a subclass relation on their indexed
       unions.  Generalization of Proposition 8.6 of [TakeutiZaring] p. 59.
       Compare ~ uniss2 . $)
    iunss2 $p |- ( A. x e. A E. y e. B C C_ D ->
                 U_ x e. A C C_ U_ y e. B D ) $=
      ( wss wrex wral ciun ssiun ralimi iunss sylibr ) EFGBDHZACIEBDFJZGZACIACE
      JPGOQACBDFEKLACEPMN $.
      $( [9-Dec-2004] $)
  $}

  ${
    $d y z A $.  $d x y z B $.  $d z ph $.
    $( The indexed union of a restricted class abstraction. $)
    iunrab $p |- U_ x e. A { y e. B | ph } = { y e. B | E. x e. A ph } $=
      ( vz cv wsbc wrex crab wcel wa cab ciun df-rab ax-17 hbs1 hbrex wceq
      sbequ12 rexbidv cbvrab eliun elrabsf rexbii r19.42v 3bitri abbi2i
      3eqtr4ri ) ACFGZHZBDIZFEJUJEKZULLZFMABDIZCEJBDACEJZNZULFEOUOULCFBEBGZEKZC
      PZUSFPUOFPUKCBDURDKCPACFQRCGUJSAUKBDACFTUAUBUNFUQUJUQKUJUPKZBDIUMUKLZBDIU
      NBUJDUPUCVAVBBDACBUJEUTUDUEUMUKBDUFUGUHUI $.
      $( [3-Jan-2004] $)
  $}

  ${
    $d y A $.  $d x y $.
    $( The indexed union of a class abstraction. $)
    iunab $p |- U_ x e. A { y | ph } = { y | E. x e. A ph } $=
      ( cvv crab ciun wrex cab iunrab wceq cv wcel rabab a1i iuneq2i 3eqtr3i )
      BDACEFZGABDHZCEFBDACIZGSCIABCDEJBDRTRTKBLDMACNOPSCNQ $.
      $( [27-Dec-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    iunxdif2.1 $e |- ( x = y -> C = D ) $.
    $( Indexed union with a class difference as its index. $)
    iunxdif2 $p |- ( A. x e. A E. y e. ( A \ B ) C C_ D ->
                 U_ y e. ( A \ B ) D = U_ x e. A C ) $=
      ( wss cdif wrex wral ciun wa wceq iunss2 difss iunss1 ax-mp cbviunv
      sseqtr4i jctil eqss sylibr ) EFHBCDIZJACKZBUDFLZACELZHZUGUFHZMUFUGNUEUIUH
      ABCUDEFOUFBCFLZUGUDCHUFUJHCDPBUDCFQRABCEFGSTUAUFUGUBUC $.
      $( [10-Dec-2004] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d x y z $.
    ssiinf.1 $e |- ( y e. C -> A. x y e. C ) $.
    $( Subset theorem for an indexed intersection.  (Contributed by FL,
       15-Oct-2012.) $)
    ssiinf $p |- ( C C_ |^|_ x e. A B <-> A. x e. A C C_ B ) $=
      ( vz ciin wss cv wcel wi wal wral ax-17 dfss2f cvv wb visset eliin ax-mp
      imbi2i albii dfss2 ralbii ralcom4 r19.21t mpg 3bitrri 3bitri ) EACDHZIBJZ
      EKZULUKKZLZBMUMULDKZACNZLZBMZEDIZACNZBGEUKGJZEKBOVBUKKBOPUOURBUNUQUMULQKU
      NUQRBSAULCDQTUAUBUCVAUMUPLZBMZACNVCACNZBMUSUTVDACBEDUDUEVCABCUFVEURBUMUMA
      MLVEURRAUMUPACUGFUHUCUIUJ $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed intersection. $)
    ssiin $p |- ( C C_ |^|_ x e. A B <-> A. x e. A C C_ B ) $=
      ( vy cv wcel ax-17 ssiinf ) AEBCDEFDGAHI $.
      $( [15-Oct-2003] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed intersection.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    iinss $p |- ( E. x e. A B C_ C -> |^|_ x e. A B C_ C ) $=
      ( vy wss wrex ciin cv wcel wral cvv wb visset eliin ax-mp wi ssel reximi
      r19.36av syl syl5bi ssrdv ) CDFZABGZEABCHZDEIZUFJZUGCJZABKZUEUGDJZUGLJUHU
      JMENAUGBCLOPUEUIUKQZABGUJUKQUDULABCDUGRSUIUKABTUAUBUC $.
      $( [25-Jul-2011] $) $( [15-Oct-2003] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    $( An indexed intersection is included in any of its members.  (Contributed
       by FL, 15-Oct-2012.) $)
    iinss2 $p |- ( x e. A -> |^|_ x e. A B C_ B ) $=
      ( vy cv wcel ciin wral wi cvv wb visset eliin ax-mp ra4 sylbi com12
      ssrdv ) AEBFZDABCGZCDEZTFZSUACFZUBUCABHZSUCIUAJFUBUDKDLAUABCJMNUCABOPQR
      $.
      $( [15-Oct-2012] $)
  $}

  ${
    $d x y A $.
    $( Class union in terms of indexed union.  Definition in [Stoll] p. 43. $)
    uniiun $p |- U. A = U_ x e. A x $=
      ( vy cuni cv wcel wrex cab ciun dfuni2 df-iun eqtr4i ) BDCEAEZFABGCHABMIC
      ABJACBMKL $.
      $( [28-Jun-1998] $)

    $( Class intersection in terms of indexed intersection.  Definition in
       [Stoll] p. 44. $)
    intiin $p |- |^| A = |^|_ x e. A x $=
      ( vy cint cv wcel wral cab ciin dfint2 df-iin eqtr4i ) BDCEAEZFABGCHABMIC
      ABJACBMKL $.
      $( [28-Jun-1998] $)

    $( An indexed union of singletons recovers the index set. $)
    iunid $p |- U_ x e. A { x } = A $=
      ( vy cv csn ciun wceq cab wrex wcel df-sn equcom abbii eqtri a1i iuneq2i
      iunab risset abid2 eqtr3i 3eqtri ) ABADZEZFABUBCDZGZCHZFUEABIZCHZBABUCUFU
      CUFGUBBJUCUDUBGZCHUFCUBKUIUECCALMNOPUEACBQUDBJZCHUHBUJUGCAUDBRMCBSTUA $.
      $( [18-Dec-2008] $) $( [6-Sep-2005] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( An indexed union of the empty set is empty.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    iun0 $p |- U_ x e. A (/) = (/) $=
      ( vy c0 ciun cv wcel wrex wn noel a1i nrex eliun mtbir 2false eqriv ) CAB
      DEZDCFZQGZRDGZSTABHTABTIAFBGRJZKLARBDMNUAOP $.
      $( [25-Jul-2011] $) $( [26-Mar-2003] $)

    $( An empty indexed union is empty.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    0iun $p |- U_ x e. (/) A = (/) $=
      ( vy c0 ciun cv wcel wrex rex0 eliun mtbir noel 2false eqriv ) CADBEZDCFZ
      OGZPDGQPBGZADHRAIAPDBJKPLMN $.
      $( [25-Jul-2011] $) $( [4-Dec-2004] $)

    $( An empty indexed intersection is the universal class. $)
    0iin $p |- |^|_ x e. (/) A = _V $=
      ( vy c0 ciin cv wcel wral cab cvv df-iin visset ral0 2th abbi2i eqtr4i )
      ADBECFZBGZADHZCIJACDBKSCJQJGSCLRAMNOP $.
      $( [20-Oct-2005] $)

    $( Indexed intersection with a universal index class.  When ` A ` doesn't
       depend on ` x ` , this evaluates to ` A ` by ~ 19.3 and ~ abid2 .  When
       ` A = x ` , this evaluates to ` (/) ` by ~ intiin and ~ intv . $)
    viin $p |- |^|_ x e. _V A = { y | A. x y e. A } $=
      ( cvv ciin cv wcel wral cab wal df-iin ralv abbii eqtri ) ADCEBFCGZADHZBI
      OAJZBIABDCKPQBOALMN $.
      $( [11-Sep-2008] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( There is a non-empty class in an indexed collection ` B ( x ) ` iff the
       indexed union of them is non-empty.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunn0 $p |- ( E. x e. A B =/= (/) <-> U_ x e. A B =/= (/) ) $=
      ( vy cv wcel wex wrex ciun c0 wne rexcom4 eliun exbii bitr4i n0 rexbii
      3bitr4i ) DEZCFZDGZABHZSABCIZFZDGZCJKZABHUCJKUBTABHZDGUETADBLUDUGDASBCMNO
      UFUAABDCPQDUCPR $.
      $( [25-Jul-2011] $) $( [15-Oct-2003] $)
  $}

  ${
    $d y z A $.  $d z ph $.  $d x y z $.
    $( Indexed intersection of a class builder. $)
    iinab $p |- |^|_ x e. A { y | ph } = { y | A. x e. A ph } $=
      ( vz cv cab wcel wral wsbc ciin df-clab ralbii abbii df-iin ax-17 hbs1
      hbral wceq sbequ12 ralbidv cbvab 3eqtr4i ) EFZACGZHZBDIZEGACUDJZBDIZEGBDU
      EKABDIZCGUGUIEUFUHBDAECLMNBEDUEOUJUICEUJEPUHCBDBFDHCPACEQRCFUDSAUHBDACETU
      AUBUC $.
      $( [6-Dec-2011] $)

    $d x A $.  $d x B $.
    $( Indexed intersection of a restricted class builder. $)
    iinrab $p |- ( A =/= (/)
          -> |^|_ x e. A { y e. B | ph } = { y e. B | A. x e. A ph } ) $=
      ( c0 wne cv wcel wa wral cab crab ciin r19.28zv abbidv wceq df-rab a1i
      iineq2i iinab eqtri 3eqtr4g ) DFGZCHEIZAJZBDKZCLZUEABDKZJZCLBDACEMZNZUICE
      MUDUGUJCUEABDOPULBDUFCLZNUHBDUKUMUKUMQBHDIACERSTUFBCDUAUBUICERUC $.
      $( [6-Dec-2011] $)

    $d y B $.
    $( Indexed intersection of a restricted class builder. $)
    iinrab2 $p |- ( |^|_ x e. A { y e. B | ph } i^i B )
                      = { y e. B | A. x e. A ph } $=
      ( crab ciin cin wral wceq c0 cvv iineq1 0iin syl6eq ineq1d incom inv1
      eqtri rzal rabid2 ralcom bitr2i sylib eqtrd wne iinrab wss ssrab2 dfss
      mpbi syl6eqr pm2.61ine ) BDACEFZGZEHZABDIZCEFZJDKDKJZUPEURUSUPLEHZEUSUOLE
      USUOBKUNGLBDKUNMBUNNOPUTELHELEQERSOUSACEIZBDIZEURJZVABDTVCUQCEIVBUQCEUAAC
      BEDUBUCUDUEDKUFZUPUREHZURVDUOUREABCDEUGPUREUHURVEJUQCEUIUREUJUKULUM $.
      $( [6-Dec-2011] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y C $.
    $( Indexed union of intersection.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iunin2 $p |- U_ x e. A ( B i^i C ) = ( B i^i U_ x e. A C ) $=
      ( vy cin ciun cv wcel wrex wa r19.42v elin rexbii eliun anbi2i 3bitr4i
      eqriv ) EABCDFZGZCABDGZFZEHZSIZABJZUCCIZUCUAIZKZUCTIUCUBIUFUCDIZKZABJUFUI
      ABJZKUEUHUFUIABLUDUJABUCCDMNUGUKUFAUCBDOPQAUCBSOUCCUAMQR $.
      $( [26-Mar-2004] $)

    $( Indexed intersection of union.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ intiin to recover
       Enderton's theorem. $)
    iinun2 $p |- |^|_ x e. A ( B u. C ) = ( B u. |^|_ x e. A C ) $=
      ( vy cun ciin cv wcel wral wo r19.32v elun ralbii cvv wb visset eliin
      ax-mp orbi2i 3bitr4i eqriv ) EABCDFZGZCABDGZFZEHZUCIZABJZUGCIZUGUEIZKZUGU
      DIZUGUFIUJUGDIZKZABJUJUNABJZKUIULUJUNABLUHUOABUGCDMNUKUPUJUGOIZUKUPPEQZAU
      GBDORSTUAUQUMUIPURAUGBUCORSUGCUEMUAUB $.
      $( [19-Aug-2004] $)

    $( Indexed union of class difference.  Generalization of half of theorem
       "De Morgan's laws" in [Enderton] p. 31.  Use ~ intiin to recover
       Enderton's theorem. $)
    iundif2 $p |- U_ x e. A ( B \ C ) = ( B \ |^|_ x e. A C ) $=
      ( vy cdif ciun ciin cv wcel wrex wn wa r19.42v eldif rexbii wral cvv wb
      visset eliin ax-mp notbii rexnal bitr4i anbi2i 3bitr4i eliun eqriv ) EABC
      DFZGZCABDHZFZEIZUJJZABKZUNCJZUNULJZLZMZUNUKJUNUMJUQUNDJZLZMZABKUQVBABKZMU
      PUTUQVBABNUOVCABUNCDOPUSVDUQUSVAABQZLVDURVEUNRJURVESETAUNBDRUAUBUCVAABUDU
      EUFUGAUNBUJUHUNCULOUGUI $.
      $( [19-Aug-2004] $)
  $}

  ${
    $d x B $.  $d y C $.  $d x D $.  $d x y $.
    $( Rearrange indexed unions over intersection. $)
    2iunin $p |- U_ x e. A U_ y e. B ( C i^i D )
        = ( U_ x e. A C i^i U_ y e. B D ) $=
      ( cin ciun wceq cv wcel iunin2 a1i iuneq2i incom eqtri 3eqtri ) ACBDEFGHZ
      HACEBDFHZGZHZSACEHZGZUBSGACRTRTIAJCKZBDEFLMNUAACSEGZHUCACTUETUEIUDESOMNAC
      SELPSUBOQ $.
      $( [18-Dec-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Indexed intersection of class difference.  Generalization of half of
       theorem "De Morgan's laws" in [Enderton] p. 31.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iindif2 $p |- ( A =/= (/) ->
                 |^|_ x e. A ( B \ C ) = ( B \ U_ x e. A C ) ) $=
      ( vy c0 wne cdif ciin ciun cv wcel wral wn wa r19.28zv eldif ralbii wrex
      eliun notbii ralnex bitr4i anbi2i 3bitr4g cvv wb visset eliin ax-mp
      eqrdv ) BFGZEABCDHZIZCABDJZHZULEKZUMLZABMZUQCLZUQUOLZNZOZUQUNLZUQUPLULUTU
      QDLZNZOZABMUTVFABMZOUSVCUTVFABPURVGABUQCDQRVBVHUTVBVEABSZNVHVAVIAUQBDTUAV
      EABUBUCUDUEUQUFLVDUSUGEUHAUQBUMUFUIUJUQCUOQUEUK $.
      $( [5-Oct-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d y V $.
    iinxsng.1 $e |- ( x = A -> B = C ) $.
    $( A singleton index picks out an instance of an indexed intersection's
       argument. $)
    iinxsng $p |- ( A e. V -> |^|_ x e. { A } B = C ) $=
      ( vy wcel csn ciin cv wceq wi wal wral cvv wb visset eliin ax-mp df-ral
      elsn imbi1i albii 3bitri ax-17 eleq2d ceqsalg syl5bb eqrdv ) BEHZGABIZCJZ
      DGKZUMHZAKZBLZUNCHZMZANZUKUNDHZUOURAULOZUPULHZURMZANUTUNPHUOVBQGRAUNULCPS
      TURAULUAVDUSAVCUQURABUBUCUDUEURVAABEVAAUFUQCDUNFUGUHUIUJ $.
      $( [15-Jan-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x y D $.  $d x y E $.  $d y V $.
    $d y W $.
    iinxprg.1 $e |- ( x = A -> C = D ) $.
    iinxprg.2 $e |- ( x = B -> C = E ) $.
    $( Indexed intersection with an unordered pair index. $)
    iinxprg $p |- ( ( A e. V /\ B e. W )
        -> |^|_ x e. { A , B } C = ( D i^i E ) ) $=
      ( vy wcel wa cpr ciin cin cv wceq wi wal ax-17 eleq2d ceqsalg bi2anan9
      wral cvv wb visset eliin ax-mp df-ral wo elpr imbi1i jaob bitri albii
      19.26 3bitri elin 3bitr4g eqrdv ) BGLZCHLZMZKABCNZDOZEFPZVEAQZBRZKQZDLZSZ
      ATZVICRZVLSZATZMZVKELZVKFLZMVKVGLZVKVHLVCVNVSVDVQVTVLVSABGVSAUAVJDEVKIUBU
      CVLVTACHVTAUAVODFVKJUBUCUDWAVLAVFUEZVIVFLZVLSZATZVRVKUFLWAWBUGKUHAVKVFDUF
      UIUJVLAVFUKWEVMVPMZATVRWDWFAWDVJVOULZVLSWFWCWGVLVIBCAUHUMUNVJVLVOUOUPUQVM
      VPAURUPUSVKEFUTVAVB $.
      $( [25-Jan-2012] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.
    iunxsn.1 $e |- A e. _V $.
    iunxsn.2 $e |- ( x = A -> B = C ) $.
    $( A singleton index picks out an instance of an indexed union's
       argument. $)
    iunxsn $p |- U_ x e. { A } B = C $=
      ( vy csn ciun cv wcel wrex wa wex eliun df-rex wceq elsn anbi1i exbii
      eleq2d ceqsexv bitri 3bitri eqriv ) GABHZCIZDGJZUGKUHCKZAUFLAJZUFKZUIMZAN
      ZUHDKZAUHUFCOUIAUFPUMUJBQZUIMZANUNULUPAUKUOUIABRSTUIUNABEUOCDUHFUAUBUCUDU
      E $.
      $( [26-Mar-2004] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Separate a union in an indexed union. $)
    iunun $p |- U_ x e. A ( B u. C ) = ( U_ x e. A B u. U_ x e. A C ) $=
      ( vy cv cun wcel wrex cab ciun wo elun rexbii r19.43 bitri abbii unab
      eqtr4i df-iun uneq12i 3eqtr4i ) EFZCDGZHZABIZEJZUCCHZABIZEJZUCDHZABIZEJZG
      ZABUDKABCKZABDKZGUGUIULLZEJUNUFUQEUFUHUKLZABIUQUEURABUCCDMNUHUKABOPQUIULE
      RSAEBUDTUOUJUPUMAEBCTAEBDTUAUB $.
      $( [27-Dec-2004] $)

    $( Separate a union in the index of an indexed union. $)
    iunxun $p |- U_ x e. ( A u. B ) C = ( U_ x e. A C u. U_ x e. B C ) $=
      ( vy cun ciun cv wcel wrex wo wa wex df-rex elun anbi1i andir bitri
      exbii 19.43 eliun orbi12i bitr4i 3bitri 3bitr4i eqriv ) EABCFZDGZABDGZACD
      GZFZEHZDIZAUGJZULUIIZULUJIZKZULUHIULUKIUNAHZUGIZUMLZAMURBIZUMLZURCIZUMLZK
      ZAMZUQUMAUGNUTVEAUTVAVCKZUMLVEUSVGUMURBCOPVAVCUMQRSVFVBAMZVDAMZKUQVBVDATU
      OVHUPVIUOUMABJVHAULBDUAUMABNRUPUMACJVIAULCDUAUMACNRUBUCUDAULUGDUAULUIUJOU
      EUF $.
      $( [26-Mar-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A relationship involving union and indexed intersection.  Exercise 23 of
       [Enderton] p. 33. $)
    iinuni $p |- ( A u. |^| B ) = |^|_ x e. B ( A u. x ) $=
      ( vy cint cun cv ciin wcel wo wral r19.32v elun ralbii visset elint2
      orbi2i 3bitr4ri cvv wb eliin ax-mp 3bitr4i eqriv ) DBCEZFZACBAGZFZHZDGZBI
      ZUJUEIZJZUJUHIZACKZUJUFIUJUIIZUKUJUGIZJZACKUKUQACKZJUOUMUKUQACLUNURACUJBU
      GMNULUSUKAUJCDOZPQRUJBUEMUJSIUPUOTUTAUJCUHSUAUBUCUD $.
      $( [25-Nov-2003] $)

    $( A relationship involving union and indexed union.  Exercise 25 of
       [Enderton] p. 33.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    iununi $p |- ( ( B = (/) -> A = (/) ) <->
                ( A u. U. B ) = U_ x e. B ( A u. x ) ) $=
      ( vy c0 wceq wi cuni cun cv ciun wcel wrex wo wb wn wne df-ne r19.45zv
      sylbir n0i con2i biorf rexbidv bitr3d syl ja bicomd elun rexbii syl6bbr
      eluni2 orbi2i bitri eliun 3bitr4g eqrdv wex wa eleq2 eluni ax-17 19.45
      3bitr4i df-rex 3bitr3g biimpd 19.39 orc simpl imim12i eximi 3syl 19.37v
      sylib 19.23adv neq0 3imtr4g con4d impbii ) CEFZBEFZGZBCHZIZACBAJZIZKZFZWC
      DWEWHWCDJZBLZWJWFLZACMZNZWJWGLZACMZWJWELZWJWHLZWCWNWKWLNZACMZWPWCWTWNWAWB
      WTWNOZWAPZCEQXACERWKWLACSTWBWKPZXAWKWBBWJUAUBXCWMWTWNXCWLWSACWKWLUCUDWKWM
      UCUEUFUGUHWOWSACWJBWFUIUJUKWQWKWJWDLZNZWNWJBWDUIZXDWMWKAWJCULUMUNAWJCWGUO
      ZUPUQWIWBWAWIWKDURWFCLZAURZWBPXBWIWKXIDWIWKXHGZAURZWKXIGWIWKWLXHUSZNZAURZ
      XHWOUSZAURZGXMXOGZAURXKWIXNXPWIWQWRXNXPWEWHWJUTXEWKXLAURZNWQXNXDXRWKAWJCV
      AUMXFWKXLAWKAVBVCVDWRWPXPXGWOACVEUNVFVGXMXOAVHXQXJAWKXMXOXHWKXLVIXHWOVJVK
      VLVMWKXHAVNVOVPDBVQACVQVRVSVT $.
      $( [25-Jul-2011] $) $( [25-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Subclass relationship for power class and union. $)
    sspwuni $p |- ( A C_ ~P B <-> U. A C_ B ) $=
      ( vx cv cpw wcel wral wss cuni visset elpw ralbii dfss3 unissb 3bitr4i )
      CDZBEZFZCAGPBHZCAGAQHAIBHRSCAPBCJKLCAQMCABNO $.
      $( [5-May-2009] $) $( [18-Jul-2006] $)

    $( Two ways to express a collection of subclasses. $)
    pwssb $p |- ( A C_ ~P B <-> A. x e. A x C_ B ) $=
      ( cpw wss cuni cv wral sspwuni unissb bitri ) BCDEBFCEAGCEABHBCIABCJK $.
      $( [19-Jul-2006] $)
  $}

  $( Relationship for power class and union. $)
  elpwuni $p |- ( B e. A -> ( A C_ ~P B <-> U. A = B ) ) $=
    ( cpw wss cuni wcel wceq sspwuni unissel expcom eqimss impbid1 syl5bb ) ABC
    DAEZBDZBAFZNBGZABHPOQOPQABIJNBKLM $.
    $( [13-May-2007] $) $( [18-Jul-2006] $)

  ${
    $d x y A $.
    $( The power class of an intersection in terms of indexed intersection.
       Exercise 24(a) of [Enderton] p. 33. $)
    iinpw $p |- ~P |^| A = |^|_ x e. A ~P x $=
      ( vy cint cpw cv ciin wss wcel wral ssint visset elpw ralbii bitr4i cvv
      wb eliin ax-mp 3bitr4i eqriv ) CBDZEZABAFZEZGZCFZUBHZUGUEIZABJZUGUCIUGUFI
      ZUHUGUDHZABJUJAUGBKUIULABUGUDCLZMNOUGUBUMMUGPIUKUJQUMAUGBUEPRSTUA $.
      $( [29-Nov-2003] $)

    $( Inclusion of an indexed union of a power class in the power class of the
       union of its index.  Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpwss $p |- U_ x e. A ~P x C_ ~P U. A $=
      ( vy cv cpw ciun cuni wss wrex wcel ssiun eliun visset elpw rexbii bitri
      uniiun sseq2i 3imtr4i ssriv ) CABADZEZFZBGZEZCDZUAHZABIZUFABUAFZHZUFUCJZU
      FUEJZABUAUFKUKUFUBJZABIUHAUFBUBLUMUGABUFUACMZNOPULUFUDHUJUFUDUNNUDUIUFABQ
      RPST $.
      $( [25-Nov-2003] $)
  $}

