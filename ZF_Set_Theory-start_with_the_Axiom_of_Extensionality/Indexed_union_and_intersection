$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Indexed union and intersection
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c U_ $. $( Underlined big cup. $)
  $c |^|_ $. $( Underlined big cap. $)

  $( Extend class notation to include indexed union.  Note:  Historically
     (prior to 21-Oct-2005), set.mm used the notation ` U. x e. A B ` , with
     the same union symbol as ~ cuni .  While that syntax was unambiguous, it
     did not allow for LALR parsing of the syntax constructions in set.mm.  The
     new syntax uses as distinguished symbol ` U_ ` instead of ` U. ` and does
     allow LALR parsing.  Thanks to Peter Backes for suggesting this change. $)
  ciun $a class U_ x e. A B $.

  $( Extend class notation to include indexed intersection.  Note:
     Historically (prior to 21-Oct-2005), set.mm used the notation
     ` |^| x e. A B ` , with the same intersection symbol as ~ cint .  Although
     that syntax was unambiguous, it did not allow for LALR parsing of the
     syntax constructions in set.mm.  The new syntax uses a distinguished
     symbol ` |^|_ ` instead of ` |^| ` and does allow LALR parsing.  Thanks to
     Peter Backes for suggesting this change. $)
  ciin $a class |^|_ x e. A B $.

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Define indexed union.  Definition of [Stoll] p. 45.  In normal use,
       ` A ` is independent of ` x ` , and ` B ` depends on ` x ` i.e. can be
       read informally as ` B ( x ) ` .  We call ` x ` the index, ` A ` the
       index set, and ` B ` the indexed set.  In most books, ` x e. A ` is
       written as a subscript or underneath a union symbol ` U. ` .  We use a
       special union symbol ` U_ ` to make it easier to distinguish from plain
       class union.  In many theorems, you will see that ` x ` and ` A ` are in
       the same distinct variable group (meaning ` A ` cannot depend on ` x ` )
       and that ` B ` and ` x ` do not share a distinct variable group (meaning
       that can be thought of as ` B ( x ) ` i.e. can be substituted with a
       class expression containing ` x ` ).  An alternate definition tying
       indexed union to ordinary union is ~ dfiun2 .  Theorem ~ uniiun provides
       a definition of ordinary union in terms of indexed union.  Theorems
       ~ fniunfv and ~ funiunfv are useful when ` B ` is a function. $)
    df-iun $a |- U_ x e. A B = { y | E. x e. A y e. B } $.

    $( Define indexed intersection.  Definition of [Stoll] p. 45.  See the
       remarks for its sibling operation of indexed union ~ df-iun .  An
       alternate definition tying indexed intersection to ordinary intersection
       is ~ dfiin2 .  Theorem ~ intiin provides a definition of ordinary
       intersection in terms of indexed intersection. $)
    df-iin $a |- |^|_ x e. A B = { y | A. x e. A y e. B } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.
    $( Membership in indexed union. $)
    eliun $p |- ( A e. U_ x e. B C <-> E. x e. B A e. C ) $=
      ( vy ciun wcel cvv wrex elisset wi cv a1i r19.23aiv wceq eleq1 rexbidv
      df-iun elab2g pm5.21nii ) BACDFZGBHGZBDGZACIZBUAJUCUBACUCUBKALCGBDJMNELZD
      GZACIUDEBUAHUEBOUFUCACUEBDPQAECDRST $.
      $( [3-Sep-2003] $)

    $( Membership in indexed intersection. $)
    eliin $p |- ( A e. D -> ( A e. |^|_ x e. B C <-> A. x e. B A e. C ) ) $=
      ( vy cv wcel wral ciin wceq eleq1 ralbidv df-iin elab2g ) FGZDHZACIBDHZAC
      IFBACDJEPBKQRACPBDLMAFCDNO $.
      $( [3-Sep-2003] $)
  $}

  ${
    $d y z A $.  $d x z B $.  $d z C $.  $d x y $.
    $( Commutation of indexed unions. $)
    iuncom $p |- U_ x e. A U_ y e. B C = U_ y e. B U_ x e. A C $=
      ( vz ciun cv wcel wrex rexcom eliun rexbii 3bitr4i eqriv ) FACBDEGZGZBDAC
      EGZGZFHZPIZACJZTRIZBDJZTQITSITEIZBDJZACJUEACJZBDJUBUDUEABCDKUAUFACBTDELMU
      CUGBDATCELMNATCPLBTDRLNO $.
      $( [27-Dec-2008] $) $( [18-Dec-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Indexed union of a constant class, i.e. where ` B ` does not depend on
       ` x ` .  (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    iunconst $p |- ( A =/= (/) -> U_ x e. A B = B ) $=
      ( vy c0 wne ciun cv wcel wrex r19.9rzv eliun syl6rbbr eqrdv ) BEFZDABCGZC
      ODHZCIZRABJQPIRABKAQBCLMN $.
      $( [25-Jul-2011] $) $( [5-Sep-2004] $)

    $( Indexed union of a constant class, i.e. where ` B ` does not depend on
       ` x ` . $)
    iunconstOLD $p |- ( A =/= (/) -> U_ x e. A B = B ) $=
      ( vy c0 wne ciun cv wcel wex wa wb n0 ibar sylbi wrex eliun df-rex
      19.41v 3bitri syl6rbbr eqrdv ) BEFZDABCGZCUCDHZCIZAHBIZAJZUFKZUEUDIZUCUHU
      FUILABMUHUFNOUJUFABPUGUFKAJUIAUEBCQUFABRUGUFASTUAUB $.
      $( [4-Jan-2007] $) $( [5-Sep-2004] $)
  $}

  ${
    $d x y $.  $d y z A $.  $d x z B $.  $d z C $.
    $( Law combining indexed union with indexed intersection.  Eq.  (14),
       p. 109, in _Set Theory, with an Introduction to Descriptive Set Theory_
       by K. Kuratowski and A. Mostowski (2nd ed., 1976).  (This theorem also
       appears as the last example at
       ~ http://en.wikipedia.org/wiki/Union%5F%28set%5Ftheory%29 .)  (The proof
       was shortened by Andrew Salmon, 25-Jul-2011.) $)
    iuniin $p |- U_ x e. A |^|_ y e. B C C_ |^|_ y e. B U_ x e. A C $=
      ( vz ciin ciun cv wcel wrex wral r19.12 cvv wb visset eliin ax-mp rexbii
      eliun ralbii 3imtr4i ssriv ) FACBDEGZHZBDACEHZGZFIZUDJZACKZUHUFJZBDLZUHUE
      JUHUGJZUHEJZBDLZACKUNACKZBDLUJULUNABCDMUIUOACUHNJZUIUOOFPZBUHDENQRSUKUPBD
      AUHCETUAUBAUHCUDTUQUMULOURBUHDUFNQRUBUC $.
      $( [25-Jul-2011] $) $( [17-Aug-2004] $)

    $( Law combining indexed union with indexed intersection.  Eq.  (14),
       p. 109, in _Set Theory, with an Introduction to Descriptive Set Theory_
       by K. Kuratowski and A. Mostowski (2nd ed., 1976).  (This theorem also
       appears as the last example at
       ~ http://en.wikipedia.org/wiki/Union%5F%28set%5Ftheory%29 .) $)
    iuniinOLD $p |- U_ x e. A |^|_ y e. B C C_ |^|_ y e. B U_ x e. A C $=
      ( vz ciin ciun cv wcel wral wrex r19.12 eliun cvv wb visset eliin ax-mp
      rexbii bitri ralbii 3imtr4i ssriv ) FACBDEGZHZBDACEHZGZFIZEJZBDKZACLZUJAC
      LZBDKZUIUFJZUIUHJZUJABCDMUOUIUEJZACLULAUICUENUQUKACUIOJZUQUKPFQZBUIDEORST
      UAUPUIUGJZBDKZUNURUPVAPUSBUIDUGORSUTUMBDAUICENUBUAUCUD $.
      $( [8-Dec-2010] $) $( [17-Aug-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Subclass theorem for indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunss1 $p |- ( A C_ B -> U_ x e. A C C_ U_ x e. B C ) $=
      ( vy wss ciun cv wcel wrex ssrexv eliun 3imtr4g ssrdv ) BCFZEABDGZACDGZOE
      HZDIZABJSACJRPIRQISABCKARBDLARCDLMN $.
      $( [25-Jul-2011] $) $( [10-Dec-2004] $)

    $( Subclass theorem for indexed union. $)
    iunss1OLD $p |- ( A C_ B -> U_ x e. A C C_ U_ x e. B C ) $=
      ( vy wss cv wcel wrex cab ciun ssel anim1d r19.22dv2 ss2abdv df-iun
      3sstr4g ) BCFZEGDHZABIZEJSACIZEJABDKACDKRTUAERSSABCRAGZBHUBCHSBCUBLMNOAEB
      DPAECDPQ $.
      $( [11-Dec-2004] $) $( [10-Dec-2004] $)

    $( Equality theorem for indexed union. $)
    iuneq1 $p |- ( A = B -> U_ x e. A C = U_ x e. B C ) $=
      ( wss wa ciun wceq iunss1 anim12i eqss 3imtr4i ) BCEZCBEZFABDGZACDGZEZPOE
      ZFBCHOPHMQNRABCDIACBDIJBCKOPKL $.
      $( [27-Jun-1998] $)

    $( Equality theorem for restricted existential quantifier. $)
    iineq1 $p |- ( A = B -> |^|_ x e. A C = |^|_ x e. B C ) $=
      ( vy wceq cv wcel wral cab ciin raleq1 abbidv df-iin 3eqtr4g ) BCFZEGDHZA
      BIZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [27-Jun-1998] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Subclass theorem for indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    ss2iun $p |- ( A. x e. A B C_ C -> U_ x e. A B C_ U_ x e. A C ) $=
      ( vy wss wral ciun cv wcel wrex wi ssel r19.20si r19.22 syl eliun
      3imtr4g ssrdv ) CDFZABGZEABCHZABDHZUAEIZCJZABKZUDDJZABKZUDUBJUDUCJUAUEUGL
      ZABGUFUHLTUIABCDUDMNUEUGABOPAUDBCQAUDBDQRS $.
      $( [25-Jul-2011] $) $( [26-Nov-2003] $)

    $( Subclass theorem for indexed union. $)
    ss2iunOLD $p |- ( A. x e. A B C_ C -> U_ x e. A B C_ U_ x e. A C ) $=
      ( vy wss wral ciun cv wcel wrex hbra1 wi ra4 ssel syl6 r19.22d eliun
      3imtr4g ssrdv ) CDFZABGZEABCHZABDHZUBEIZCJZABKUEDJZABKUEUCJUEUDJUBUFUGABU
      AABLUBAIBJUAUFUGMUAABNCDUEOPQAUEBCRAUEBDRST $.
      $( [30-Nov-2003] $) $( [26-Nov-2003] $)

    $( Equality theorem for indexed union. $)
    iuneq2 $p |- ( A. x e. A B = C -> U_ x e. A B = U_ x e. A C ) $=
      ( wss wral wa ciun wceq ss2iun anim12i eqss ralbii r19.26 bitri 3imtr4i
      ) CDEZABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQS
      GZABFUAUFUHABCDLMQSABNOUBUCLP $.
      $( [3-Nov-2003] $) $( [22-Oct-2003] $)

    $( Equality theorem for indexed intersection.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    iineq2 $p |- ( A. x e. A B = C -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( vy wceq wral cv wcel cab ciin wb eleq2 r19.20si r19.15 syl abbidv
      df-iin 3eqtr4g ) CDFZABGZEHZCIZABGZEJUBDIZABGZEJABCKABDKUAUDUFEUAUCUELZAB
      GUDUFLTUGABCDUBMNUCUEABOPQAEBCRAEBDRS $.
      $( [25-Jul-2011] $) $( [22-Oct-2003] $)

    $( Equality theorem for indexed intersection. $)
    iineq2OLD $p |- ( A. x e. A B = C -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( vy wceq wral cv wcel cab ciin hbra1 wb ra4 eleq2 syl6 imp ralbida
      abbidv df-iin 3eqtr4g ) CDFZABGZEHZCIZABGZEJUDDIZABGZEJABCKABDKUCUFUHEUCU
      EUGABUBABLUCAHBIZUEUGMZUCUIUBUJUBABNCDUDOPQRSAEBCTAEBDTUA $.
      $( [4-Nov-2003] $) $( [22-Oct-2003] $)
  $}

  ${
    iuneq2i.1 $e |- ( x e. A -> B = C ) $.
    $( Equality inference for indexed union. $)
    iuneq2i $p |- U_ x e. A B = U_ x e. A C $=
      ( wceq ciun iuneq2 mprg ) CDFABCGABDGFABABCDHEI $.
      $( [3-Nov-2003] $) $( [22-Oct-2003] $)

    $( Equality inference for indexed intersection. $)
    iineq2i $p |- |^|_ x e. A B = |^|_ x e. A C $=
      ( wceq ciin iineq2 mprg ) CDFABCGABDGFABABCDHEI $.
      $( [4-Nov-2003] $) $( [22-Oct-2003] $)
  $}

  ${
    $d x ph $.
    iuneq2dv.1 $e |- ( ( ph /\ x e. A ) -> B = C ) $.
    $( Equality deduction for indexed union. $)
    iuneq2dv $p |- ( ph -> U_ x e. A B = U_ x e. A C ) $=
      ( wceq wral ciun r19.21aiva iuneq2 syl ) ADEGZBCHBCDIBCEIGAMBCFJBCDEKL $.
      $( [5-Oct-2006] $) $( [3-Aug-2004] $)

    $( Equality deduction for indexed intersection. $)
    iineq2dv $p |- ( ph -> |^|_ x e. A B = |^|_ x e. A C ) $=
      ( wceq wral ciin r19.21aiva iineq2 syl ) ADEGZBCHBCDIBCEIGAMBCFJBCDEKL $.
      $( [3-Jan-2007] $) $( [3-Aug-2004] $)
  $}

  ${
    $d z A $.  $d z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed union. $)
    hbiu1 $p |- ( y e. U_ x e. A B -> A. x y e. U_ x e. A B ) $=
      ( vz ciun cv wcel wrex cab df-iun hbre1 hbab hbxfr ) ABACDFEGDHZACIZEJAEC
      DKPAEBOACLMN $.
      $( [14-Oct-2003] $) $( [12-Oct-2003] $)
  $}

  ${
    $d z A $.  $d z B $.  $d x y z $.
    $( Bound-variable hypothesis builder for indexed intersection. $)
    hbii1 $p |- ( y e. |^|_ x e. A B -> A. x y e. |^|_ x e. A B ) $=
      ( vz ciin cv wcel wral cab df-iin hbra1 hbab hbxfr ) ABACDFEGDHZACIZEJAEC
      DKPAEBOACLMN $.
      $( [16-Oct-2003] $) $( [15-Oct-2003] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w C z $.  $d w x y z $.
    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    dfiun2g $p |- ( A. x e. A B e. C ->
                  U_ x e. A B = U. { y | E. x e. A y = B } ) $=
      ( vz wcel wral ciun cv wceq wrex cab cuni wa wex hbra1 wb ra4 clel3g
      syl6 imp rexbida rexcom4 syl6bb r19.41v exbii exancom bitri eliun
      eluniab 3bitr4g eqrdv ) DEGZACHZFACDIZBJZDKZACLZBMNZUOFJZDGZACLZVAUQGZUSO
      BPZVAUPGVAUTGUOVCURVDOZACLZBPZVEUOVCVFBPZACLVHUOVBVIACUNACQUOAJCGZVBVIRZU
      OVJUNVKUNACSBVADETUAUBUCVFABCUDUEVHUSVDOZBPVEVGVLBURVDACUFUGUSVDBUHUIUEAV
      ACDUJUSBVAUKULUM $.
      $( [25-Jul-2011] $) $( [23-Mar-2006] $)

    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44. $)
    dfiun2gOLD $p |- ( A. x e. A B e. C ->
                  U_ x e. A B = U. { y | E. x e. A y = B } ) $=
      ( vw vz wcel wral cv wrex cab wceq wa wex ciun cuni hbra1 wb ra4 clel3g
      exancom syl6bb syl6 pm5.32d 19.42v syl6bbr exbid excom an12 exbii visset
      eqeq1 rexbidv elab df-rex bitri anbi2i 3bitr4i syl5bb abbidv df-iun
      df-uni 3eqtr4g ) DEHZACIZFJZDHZACKZFLVGGJZHZVJBJZDMZACKZBLZHZNZGOZFLACDPV
      OQVFVIVRFVFAJCHZVHNZAOZVRVIVFWAVSVKVJDMZNZNZGOZAOZVRVFVTWEAVEACRVFVTVSWCG
      OZNWEVFVSVHWGVFVSVEVHWGSVEACTVEVHWBVKNGOWGGVGDEUAWBVKGUBUCUDUEVSWCGUFUGUH
      WFWDAOZGOVRWDAGUIWHVQGVKVSWBNZNZAOVKWIAOZNWHVQVKWIAUFWDWJAVSVKWBUJUKVPWKV
      KVPWBACKZWKVNWLBVJGULVLVJMVMWBACVLVJDUMUNUOWBACUPUQURUSUKUQUCVHACUPUTVAAF
      CDVBFGVOVCVD $.
      $( [25-Mar-2006] $) $( [23-Mar-2006] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.
    dfiun2.1 $e |- B e. _V $.
    $( Alternate definition of indexed union when ` B ` is a set.  Definition
       15(a) of [Suppes] p. 44. $)
    dfiun2 $p |- U_ x e. A B = U. { y | E. x e. A y = B } $=
      ( cvv wcel ciun cv wceq wrex cab cuni dfiun2g a1i mprg ) DFGZACDHBIDJACKB
      LMJACABCDFNQAICGEOP $.
      $( [27-Jun-1998] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w x y z $.
    dfiin2.1 $e |- B e. _V $.
    $( Alternate definition of indexed intersection when ` B ` is a set.
       Definition 15(b) of [Suppes] p. 44.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    dfiin2 $p |- |^|_ x e. A B = |^| { y | E. x e. A y = B } $=
      ( vz ciin cv wceq wrex cab cint wcel wral wi wal clel4 ralbii ralcom4
      r19.23v albii 3bitri cvv wb visset eliin ax-mp elintab 3bitr4i eqriv ) FA
      CDGZBHZDIZACJZBKLZFHZDMZACNZUNUPULMZOZBPZUPUKMZUPUOMURUMUSOZBPZACNVCACNZB
      PVAUQVDACBUPDEQRVCABCSVEUTBUMUSACTUAUBUPUCMVBURUDFUEZAUPCDUCUFUGUNBUPVFUH
      UIUJ $.
      $( [25-Jul-2011] $) $( [28-Jun-1998] $)

    $( Alternate definition of indexed intersection when ` B ` is a set.
       Definition 15(b) of [Suppes] p. 44. $)
    dfiin2OLD $p |- |^|_ x e. A B = |^| { y | E. x e. A y = B } $=
      ( vw vz cv wcel wral cab wceq wrex wi wal ciin cint df-ral clel4 imbi2i
      19.21v bitr4i albii alcom bitri wa wex visset eqeq1 rexbidv elab df-rex
      imbi1i 19.23v impexp 3bitr2ri 3bitri abbii df-iin df-int 3eqtr4i ) FHZDIZ
      ACJZFKGHZBHZDLZACMZBKZIZVBVEIZNZGOZFKACDPVIQVDVMFVDAHCIZVCNZAOZVNVEDLZVKN
      ZNZAOZGOZVMVCACRVPVSGOZAOWAVOWBAVOVNVRGOZNWBVCWCVNGVBDESTVNVRGUAUBUCVSAGU
      DUEVTVLGVLVNVQUFZAUGZVKNWDVKNZAOVTVJWEVKVJVQACMZWEVHWGBVEGUHVFVELVGVQACVF
      VEDUIUJUKVQACULUEUMWDVKAUNWFVSAVNVQVKUOUCUPUCUQURAFCDUSFGVIUTVA $.
      $( [28-Jun-1998] $)
  $}

  ${
    $d w y A $.  $d w x A $.  $d w z B $.  $d w z C $.
    cbviun.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbviun.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbviun.3 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       25-Jul-2011.) $)
    cbviun $p |- U_ x e. A B = U_ y e. A C $=
      ( vw ciun cv wcel wrex hblem wceq eleq2d cbvrex eliun 3bitr4i eqriv ) JAD
      EKZBDFKZJLZEMZADNUDFMZBDNUDUBMUDUCMUEUFABDBCJEGOACJFHOALBLPEFUDIQRAUDDESB
      UDDFSTUA $.
      $( [25-Jul-2011] $) $( [26-Mar-2006] $)
  $}

  ${
    $d x A $.  $d y A $.  $d y z B $.  $d x z C $.
    cbviunv.1 $e |- ( x = y -> B = C ) $.
    $( Rule used to change the bound variables in an indexed union, with the
       substitution specified implicitly by the hypothesis. $)
    cbviunv $p |- U_ x e. A B = U_ y e. A C $=
      ( vz cv wcel ax-17 cbviun ) ABGCDEGHZDIBJLEIAJFK $.
      $( [25-Jul-2011] $) $( [15-Sep-2003] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed union.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunss $p |- ( U_ x e. A B C_ C <-> A. x e. A B C_ C ) $=
      ( vy ciun wss cv wcel wrex cab wi wal wral df-iun sseq1i abss dfss2
      ralbii ralcom4 r19.23v albii 3bitrri 3bitri ) ABCFZDGEHZCIZABJZEKZDGUHUFD
      IZLZEMZCDGZABNZUEUIDAEBCOPUHEDQUNUGUJLZEMZABNUOABNZEMULUMUPABECDRSUOAEBTU
      QUKEUGUJABUAUBUCUD $.
      $( [25-Jul-2011] $) $( [13-Sep-2003] $)

    $( Subset theorem for an indexed union. $)
    iunssOLD $p |- ( U_ x e. A B C_ C <-> A. x e. A B C_ C ) $=
      ( vy wss wral cv wcel wa wi wal ciun dfss2 ralbii df-ral impexp albii
      19.21v bitr2i 3bitri wex 19.23v wrex eliun df-rex bitri imbi1i bitr4i
      alcom 3bitr4i ) CDFZABGZAHBIZEHZCIZJZUODIZKZELZALZABCMZDFZUMUPURKZELZABGU
      NVEKZALVAULVEABECDNOVEABPVFUTAUTUNVDKZELVFUSVGEUNUPURQRUNVDESTRUAUSALZELU
      OVBIZURKZELVAVCVHVJEVHUQAUBZURKVJUQURAUCVIVKURVIUPABUDVKAUOBCUEUPABUFUGUH
      UIRUSAEUJEVBDNUKT $.
      $( [13-Sep-2003] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed union.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    ssiun $p |- ( E. x e. A C C_ B -> C C_ U_ x e. A B ) $=
      ( vy wss wrex ciun cv wcel wi ssel r19.22si r19.37av syl eliun syl6ibr
      ssrdv ) DCFZABGZEDABCHZTEIZDJZUBCJZABGZUBUAJTUCUDKZABGUCUEKSUFABDCUBLMUCU
      DABNOAUBBCPQR $.
      $( [25-Jul-2011] $) $( [3-Sep-2003] $)

    $( Subset implication for an indexed union. $)
    ssiunOLD $p |- ( E. x e. A C C_ B -> C C_ U_ x e. A B ) $=
      ( vy wss wrex cv wcel wa wex ciun df-rex wi wal pm3.35 anim2i exp32
      com23 imp ssel sylan2 19.22i 19.21aiv eliun bitr2i imbi2i albii 19.37v
      dfss2 3bitr4i sylib sylbi ) DCFZABGAHBIZUNJZAKZDABCLZFZUNABMUQEHZDIZUOUTC
      IZJZNZAKZEOZUSUQVEEUPVDAUOVAVBNZVDUNUOVGVDUOVAVGVCUOVAVGVCVAVGJVBUOVAVBPQ
      RSTDCUTUAUBUCUDVAVCAKZNZEOVAUTURIZNZEOVFUSVIVKEVHVJVAVJVBABGVHAUTBCUEVBAB
      MUFUGUHVEVIEVAVCAUIUHEDURUJUKULUM $.
      $( [3-Sep-2003] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Identity law for subset of an indexed union.  (The proof was shortened
       by Andrew Salmon, 25-Jul-2011.) $)
    ssiun2 $p |- ( x e. A -> B C_ U_ x e. A B ) $=
      ( vy cv wcel ciun wrex ra4e ex eliun syl6ibr ssrdv ) AEBFZDCABCGZNDEZCFZQ
      ABHZPOFNQRQABIJAPBCKLM $.
      $( [25-Jul-2011] $) $( [12-Oct-2003] $)

    $( Identity law for subset of an indexed union. $)
    ssiun2OLD $p |- ( x e. A -> B C_ U_ x e. A B ) $=
      ( vy cv wcel ciun wa wrex ra4e eliun sylibr ex ssrdv ) AEBFZDCABCGZODEZCF
      ZQPFZORHRABISRABJAQBCKLMN $.
      $( [14-Oct-2003] $) $( [12-Oct-2003] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.
    ssiun2s.1 $e |- ( x = C -> B = D ) $.
    $( Subset relationship for an indexed union. $)
    ssiun2s $p |- ( C e. A -> D C_ U_ x e. A B ) $=
      ( vy wcel ciun wss cv wi ax-17 hbiu1 hbss hbim wceq eleq1 sseq1d imbi12d
      ssiun2 vtoclgf pm2.43i ) DBHZEABCIZJZAKZBHZCUEJZLUDUFLAGDBGKZDHAMUDUFAUDA
      MAGEUEUJEHAMAGBCNOPUGDQZUHUDUIUFUGDBRUKCEUEFSTABCUAUBUC $.
      $( [2-Nov-2003] $) $( [26-Oct-2003] $)
  $}

  ${
    $d x y $.  $d x B $.  $d y C $.  $d x D $.
    $( A subclass condition on the members of two indexed classes ` C ( x ) `
       and ` D ( y ) ` that implies a subclass relation on their indexed
       unions.  Generalization of Proposition 8.6 of [TakeutiZaring] p. 59.
       Compare ~ uniss2 . $)
    iunss2 $p |- ( A. x e. A E. y e. B C C_ D ->
                 U_ x e. A C C_ U_ y e. B D ) $=
      ( wss wrex wral ciun ssiun r19.20si iunss sylibr ) EFGBDHZACIEBDFJZGZACIA
      CEJPGOQACBDFEKLACEPMN $.
      $( [30-Dec-2004] $) $( [9-Dec-2004] $)
  $}

  ${
    $d y z A $.  $d x y z B $.  $d z ph $.
    $( The indexed union of a restricted class abstraction. $)
    iunrab $p |- U_ x e. A { y e. B | ph } = { y e. B | E. x e. A ph } $=
      ( vz cv wsbc wrex crab wcel wa cab ciun df-rab ax-17 hbs1 hbrex wceq
      sbequ12 rexbidv cbvrab eliun elrabsf rexbii r19.42v 3bitri abbi2i
      3eqtr4ri ) ACFGZHZBDIZFEJUJEKZULLZFMABDIZCEJBDACEJZNZULFEOUOULCFBEBGZEKZC
      PZUSFPUOFPUKCBDURDKCPACFQRCGUJSAUKBDACFTUAUBUNFUQUJUQKUJUPKZBDIUMUKLZBDIU
      NBUJDUPUCVAVBBDACBUJEUTUDUEUMUKBDUFUGUHUI $.
      $( [24-Jan-2004] $) $( [3-Jan-2004] $)
  $}

  ${
    $d y A $.  $d x y $.
    $( The indexed union of a class abstraction. $)
    iunab $p |- U_ x e. A { y | ph } = { y | E. x e. A ph } $=
      ( cvv crab ciun wrex cab iunrab wceq cv wcel rabab a1i iuneq2i 3eqtr3i )
      BDACEFZGABDHZCEFBDACIZGSCIABCDEJBDRTRTKBLDMACNOPSCNQ $.
      $( [2-Jan-2005] $) $( [27-Dec-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.  $d x D $.
    iunxdif2.1 $e |- ( x = y -> C = D ) $.
    $( Indexed union with a class difference as its index. $)
    iunxdif2 $p |- ( A. x e. A E. y e. ( A \ B ) C C_ D ->
                 U_ y e. ( A \ B ) D = U_ x e. A C ) $=
      ( wss cdif wrex wral ciun wa wceq iunss2 difss iunss1 ax-mp cbviunv
      sseqtr4i jctil eqss sylibr ) EFHBCDIZJACKZBUDFLZACELZHZUGUFHZMUFUGNUEUIUH
      ABCUDEFOUFBCFLZUGUDCHUFUJHCDPBUDCFQRABCEFGSTUAUFUGUBUC $.
      $( [24-Feb-2005] $) $( [10-Dec-2004] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset theorem for an indexed intersection.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    ssiin $p |- ( C C_ |^|_ x e. A B <-> A. x e. A C C_ B ) $=
      ( vy ciin wss cv wcel wi wal wral dfss2 cvv wb visset eliin ax-mp imbi2i
      albii ralbii ralcom4 r19.21v 3bitrri bitri ) DABCFZGEHZDIZUGUFIZJZEKZDCGZ
      ABLZEDUFMUKUHUGCIZABLZJZEKZUMUJUPEUIUOUHUGNIUIUOOEPAUGBCNQRSTUMUHUNJZEKZA
      BLURABLZEKUQULUSABEDCMUAURAEBUBUTUPEUHUNABUCTUDUEUE $.
      $( [25-Jul-2011] $) $( [15-Oct-2003] $)

    $( Subset theorem for an indexed intersection. $)
    ssiinOLD $p |- ( C C_ |^|_ x e. A B <-> A. x e. A C C_ B ) $=
      ( vy cv wcel ciin wi wal wss wral cvv wb visset eliin ax-mp imbi2i
      r19.21v df-ral 3bitr2i albii dfss2 ralbii 19.21v alcom bitr3i 3bitri
      3bitr4i ) EFZDGZUJABCHZGZIZEJAFBGZUKUJCGZIZIZAJZEJZDULKDCKZABLZUNUSEUNUKU
      PABLZIUQABLUSUMVCUKUJMGUMVCNEOAUJBCMPQRUKUPABSUQABTUAUBEDULUCVBUQEJZABLUO
      VDIZAJZUTVAVDABEDCUCUDVDABTVFUREJZAJUTVGVEAUOUQEUEUBURAEUFUGUHUI $.
      $( [20-Oct-2003] $) $( [15-Oct-2003] $)
  $}

  ${
    $d x y C $.  $d y A $.  $d y B $.
    $( Subset implication for an indexed intersection.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    iinss $p |- ( E. x e. A B C_ C -> |^|_ x e. A B C_ C ) $=
      ( vy wss wrex ciin cv wcel wral wi ssel r19.22si r19.36av syl cvv wb
      visset eliin ax-mp syl5ib ssrdv ) CDFZABGZEABCHZDUEEIZCJZABKZUGDJZUGUFJZU
      EUHUJLZABGUIUJLUDULABCDUGMNUHUJABOPUGQJUKUIRESAUGBCQTUAUBUC $.
      $( [25-Jul-2011] $) $( [15-Oct-2003] $)

    $( Subset implication for an indexed intersection. $)
    iinssOLD $p |- ( E. x e. A B C_ C -> |^|_ x e. A B C_ C ) $=
      ( vy cv wcel wi wal wrex ciin wss wa wex 19.12 df-rex 19.28v exbii
      bitr4i albii 3imtr4i wral r19.36av cvv wb visset eliin ax-mp syl5ib
      19.20i syl dfss2 rexbii ) EFZCGZUNDGZHZEIZABJZUNABCKZGZUPHZEIZCDLZABJUTDL
      USUQABJZEIZVCAFBGZUQMZEIZANZVHANZEIUSVFVHAEOUSVGURMZANVJURABPVIVLAVGUQEQR
      SVEVKEUQABPTUAVEVBEVEUOABUBZUPVAUOUPABUCUNUDGVAVMUEEUFAUNBCUDUGUHUIUJUKVD
      URABECDULUMEUTDULUA $.
      $( [22-Oct-2003] $) $( [15-Oct-2003] $)
  $}

  ${
    $d x y A $.
    $( Class union in terms of indexed union.  Definition of [Stoll] p. 43. $)
    uniiun $p |- U. A = U_ x e. A x $=
      ( vy cuni cv wcel wrex cab ciun dfuni2 df-iun eqtr4i ) BDCEAEZFABGCHABMIC
      ABJACBMKL $.
      $( [28-Jun-1998] $)

    $( Class intersection in terms of indexed intersection.  Definition of
       [Stoll] p. 44. $)
    intiin $p |- |^| A = |^|_ x e. A x $=
      ( vy cint cv wcel wral cab ciin dfint2 df-iin eqtr4i ) BDCEAEZFABGCHABMIC
      ABJACBMKL $.
      $( [28-Jun-1998] $)

    $( An indexed union of singletons recovers the index set. $)
    iunid $p |- U_ x e. A { x } = A $=
      ( vy cv csn ciun wceq cab wrex wcel df-sn equcom abbii eqtri a1i iuneq2i
      iunab risset abid2 eqtr3i 3eqtri ) ABADZEZFABUBCDZGZCHZFUEABIZCHZBABUCUFU
      CUFGUBBJUCUDUBGZCHUFCUBKUIUECCALMNOPUEACBQUDBJZCHUHBUJUGCAUDBRMCBSTUA $.
      $( [18-Dec-2008] $) $( [6-Sep-2005] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( An indexed union of the empty set is empty.  (The proof was shortened by
       Andrew Salmon, 25-Jul-2011.) $)
    iun0 $p |- U_ x e. A (/) = (/) $=
      ( vy c0 ciun cv wcel wrex wn noel a1i nrex eliun mtbir 2false eqriv ) CAB
      DEZDCFZQGZRDGZSTABHTABTIAFBGRJZKLARBDMNUAOP $.
      $( [25-Jul-2011] $) $( [26-Mar-2003] $)

    $( An indexed union of the empty set is empty. $)
    iun0OLD $p |- U_ x e. A (/) = (/) $=
      ( vy c0 ciun cv wcel wrex eliun wn noel a1i nrex 2false bitri eqriv ) CAB
      DEZDCFZQGRDGZABHZSARBDITSSABSJAFBGRKZLMUANOP $.
      $( [27-Mar-2003] $) $( [26-Mar-2003] $)

    $( An empty indexed union is empty.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    0iun $p |- U_ x e. (/) A = (/) $=
      ( vy c0 ciun cv wcel wrex rex0 eliun mtbir noel 2false eqriv ) CADBEZDCFZ
      OGZPDGQPBGZADHRAIAPDBJKPLMN $.
      $( [25-Jul-2011] $) $( [4-Dec-2004] $)

    $( An empty indexed union is empty. $)
    0iunOLD $p |- U_ x e. (/) A = (/) $=
      ( vy c0 ciun cv wcel wrex eliun rex0 noel 2false bitri eqriv ) CADBEZDCFZ
      OGPBGZADHZPDGZAPDBIRSQAJPKLMN $.
      $( [21-Feb-2005] $) $( [4-Dec-2004] $)

    $( An empty indexed intersection is the universal class. $)
    0iin $p |- |^|_ x e. (/) A = _V $=
      ( vy c0 ciin cv wcel wral cab cvv df-iin visset ral0 2th abbi2i eqtr4i )
      ADBECFZBGZADHZCIJACDBKSCJQJGSCLRAMNOP $.
      $( [22-Nov-2005] $) $( [20-Oct-2005] $)

    $( Indexed intersection with a universal index class.  When ` A ` doesn't
       depend on ` x ` , this evaluates to ` A ` by ~ 19.3 and ~ abid2 .  When
       ` A = x ` , this evaluates to ` (/) ` by ~ intiin and ~ intv . $)
    viin $p |- |^|_ x e. _V A = { y | A. x y e. A } $=
      ( cvv ciin cv wcel wral cab wal df-iin ralv abbii eqtri ) ADCEBFCGZADHZBI
      OAJZBIABDCKPQBOALMN $.
      $( [11-Sep-2008] $) $( [11-Sep-2008] $)
  $}

  ${
    $d x y A $.  $d y B $.
    $( There is a non-empty class in an indexed collection ` B ( x ) ` iff the
       indexed union of them is non-empty.  (The proof was shortened by Andrew
       Salmon, 25-Jul-2011.) $)
    iunn0 $p |- ( E. x e. A B =/= (/) <-> U_ x e. A B =/= (/) ) $=
      ( vy cv wcel wex wrex ciun c0 wne rexcom4 eliun exbii bitr4i n0 rexbii
      3bitr4i ) DEZCFZDGZABHZSABCIZFZDGZCJKZABHUCJKUBTABHZDGUETADBLUDUGDASBCMNO
      UFUAABDCPQDUCPR $.
      $( [25-Jul-2011] $) $( [15-Oct-2003] $)

    $( There is a non-empty class in an indexed collection ` B ( x ) ` iff the
       indexed union of them is non-empty. $)
    iunn0OLD $p |- ( E. x e. A B =/= (/) <-> U_ x e. A B =/= (/) ) $=
      ( vy c0 wne wrex cv wcel wex wa ciun n0 rexbii df-rex excom exdistr
      eliun bitr2i exbii 3bitr3i bitr4i 3bitri ) CEFZABGDHZCIZDJZABGAHBIZUGKAJZ
      ABCLZEFZUDUGABDCMNUGABOUIUEUJIZDJZUKUHUFKZDJAJUNAJZDJUIUMUNADPUHUFADQUOUL
      DULUFABGUOAUEBCRUFABOSTUADUJMUBUC $.
      $( [17-May-2007] $) $( [15-Oct-2003] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y C $.
    $( Indexed union of intersection.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iunin2 $p |- U_ x e. A ( B i^i C ) = ( B i^i U_ x e. A C ) $=
      ( vy cin ciun cv wcel wrex wa r19.42v elin rexbii eliun anbi2i 3bitr4i
      eqriv ) EABCDFZGZCABDGZFZEHZSIZABJZUCCIZUCUAIZKZUCTIUCUBIUFUCDIZKZABJUFUI
      ABJZKUEUHUFUIABLUDUJABUCCDMNUGUKUFAUCBDOPQAUCBSOUCCUAMQR $.
      $( [30-Mar-2004] $) $( [26-Mar-2004] $)

    $( Indexed intersection of union.  Generalization of half of theorem
       "Distributive laws" in [Enderton] p. 30.  Use ~ intiin to recover
       Enderton's theorem. $)
    iinun2 $p |- |^|_ x e. A ( B u. C ) = ( B u. |^|_ x e. A C ) $=
      ( vy cun ciin cv wcel wral wo r19.32v elun ralbii cvv wb visset eliin
      ax-mp orbi2i 3bitr4i eqriv ) EABCDFZGZCABDGZFZEHZUCIZABJZUGCIZUGUEIZKZUGU
      DIZUGUFIUJUGDIZKZABJUJUNABJZKUIULUJUNABLUHUOABUGCDMNUKUPUJUGOIZUKUPPEQZAU
      GBDORSTUAUQUMUIPURAUGBUCORSUGCUEMUAUB $.
      $( [3-Sep-2004] $) $( [19-Aug-2004] $)

    $( Indexed union of class difference.  Generalization of half of theorem
       "De Morgan's laws" in [Enderton] p. 31.  Use ~ intiin to recover
       Enderton's theorem. $)
    iundif2 $p |- U_ x e. A ( B \ C ) = ( B \ |^|_ x e. A C ) $=
      ( vy cdif ciun ciin cv wcel wrex wn wa r19.42v eldif rexbii wral cvv wb
      visset eliin ax-mp notbii rexnal bitr4i anbi2i 3bitr4i eliun eqriv ) EABC
      DFZGZCABDHZFZEIZUJJZABKZUNCJZUNULJZLZMZUNUKJUNUMJUQUNDJZLZMZABKUQVBABKZMU
      PUTUQVBABNUOVCABUNCDOPUSVDUQUSVAABQZLVDURVEUNRJURVESETAUNBDRUAUBUCVAABUDU
      EUFUGAUNBUJUHUNCULOUGUI $.
      $( [30-Oct-2004] $) $( [19-Aug-2004] $)
  $}

  ${
    $d x B $.  $d y C $.  $d x D $.  $d x y $.
    $( Rearrange indexed unions over intersection. $)
    2iunin $p |- U_ x e. A U_ y e. B ( C i^i D )
        = ( U_ x e. A C i^i U_ y e. B D ) $=
      ( cin ciun wceq cv wcel iunin2 a1i iuneq2i incom eqtri 3eqtri ) ACBDEFGHZ
      HACEBDFHZGZHZSACEHZGZUBSGACRTRTIAJCKZBDEFLMNUAACSEGZHUCACTUETUEIUDESOMNAC
      SELPSUBOQ $.
      $( [19-Jan-2009] $) $( [18-Dec-2008] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y C $.
    $( Indexed intersection of class difference.  Generalization of half of
       theorem "De Morgan's laws" in [Enderton] p. 31.  Use ~ uniiun to recover
       Enderton's theorem. $)
    iindif2 $p |- ( A =/= (/) ->
                 |^|_ x e. A ( B \ C ) = ( B \ U_ x e. A C ) ) $=
      ( vy c0 wne cdif ciin ciun cv wcel wral wn wa r19.28zv eldif ralbii wrex
      eliun notbii ralnex bitr4i anbi2i 3bitr4g cvv wb visset eliin ax-mp
      eqrdv ) BFGZEABCDHZIZCABDJZHZULEKZUMLZABMZUQCLZUQUOLZNZOZUQUNLZUQUPLULUTU
      QDLZNZOZABMUTVFABMZOUSVCUTVFABPURVGABUQCDQRVBVHUTVBVEABSZNVHVAVIAUQBDTUAV
      EABUBUCUDUEUQUFLVDUSUGEUHAUQBUMUFUIUJUQCUOQUEUK $.
      $( [13-Oct-2006] $) $( [5-Oct-2006] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.
    iunxsn.1 $e |- A e. _V $.
    iunxsn.2 $e |- ( x = A -> B = C ) $.
    $( A singleton index picks out an instance of an indexed union's
       argument. $)
    iunxsn $p |- U_ x e. { A } B = C $=
      ( vy csn ciun cv wcel wrex wa wex eliun df-rex wceq elsn anbi1i exbii
      eleq2d ceqsexv bitri 3bitri eqriv ) GABHZCIZDGJZUGKUHCKZAUFLAJZUFKZUIMZAN
      ZUHDKZAUHUFCOUIAUFPUMUJBQZUIMZANUNULUPAUKUOUIABRSTUIUNABEUOCDUHFUAUBUCUDU
      E $.
      $( [27-Mar-2004] $) $( [26-Mar-2004] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Separate a union in an indexed union. $)
    iunun $p |- U_ x e. A ( B u. C ) = ( U_ x e. A B u. U_ x e. A C ) $=
      ( vy cv cun wcel wrex cab ciun wo elun rexbii r19.43 bitri abbii unab
      eqtr4i df-iun uneq12i 3eqtr4i ) EFZCDGZHZABIZEJZUCCHZABIZEJZUCDHZABIZEJZG
      ZABUDKABCKZABDKZGUGUIULLZEJUNUFUQEUFUHUKLZABIUQUEURABUCCDMNUHUKABOPQUIULE
      RSAEBUDTUOUJUPUMAEBCTAEBDTUAUB $.
      $( [16-Jan-2005] $) $( [27-Dec-2004] $)

    $( Separate a union in the index of an indexed union. $)
    iunxun $p |- U_ x e. ( A u. B ) C = ( U_ x e. A C u. U_ x e. B C ) $=
      ( vy cun ciun cv wcel wrex wo wa wex df-rex elun anbi1i andir bitri
      exbii 19.43 eliun orbi12i bitr4i 3bitri 3bitr4i eqriv ) EABCFZDGZABDGZACD
      GZFZEHZDIZAUGJZULUIIZULUJIZKZULUHIULUKIUNAHZUGIZUMLZAMURBIZUMLZURCIZUMLZK
      ZAMZUQUMAUGNUTVEAUTVAVCKZUMLVEUSVGUMURBCOPVAVCUMQRSVFVBAMZVDAMZKUQVBVDATU
      OVHUPVIUOUMABJVHAULBDUAUMABNRUPUMACJVIAULCDUAUMACNRUBUCUDAULUGDUAULUIUJOU
      EUF $.
      $( [30-Mar-2004] $) $( [26-Mar-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( A relationship involving union and indexed intersection.  Exercise 23 of
       [Enderton] p. 33. $)
    iinuni $p |- ( A u. |^| B ) = |^|_ x e. B ( A u. x ) $=
      ( vy cint cun cv ciin wcel wo wral r19.32v elun ralbii visset elint2
      orbi2i 3bitr4ri cvv wb eliin ax-mp 3bitr4i eqriv ) DBCEZFZACBAGZFZHZDGZBI
      ZUJUEIZJZUJUHIZACKZUJUFIUJUIIZUKUJUGIZJZACKUKUQACKZJUOUMUKUQACLUNURACUJBU
      GMNULUSUKAUJCDOZPQRUJBUEMUJSIUPUOTUTAUJCUHSUAUBUCUD $.
      $( [27-Nov-2003] $) $( [25-Nov-2003] $)

    $( A relationship involving union and indexed union.  Exercise 25 of
       [Enderton] p. 33.  (The proof was shortened by Andrew Salmon,
       25-Jul-2011.) $)
    iununi $p |- ( ( B = (/) -> A = (/) ) <->
                ( A u. U. B ) = U_ x e. B ( A u. x ) ) $=
      ( vy c0 wceq wi cuni cun cv ciun wcel wrex wo wb wn wne df-ne r19.45zv
      sylbir n0i con2i biorf rexbidv bitr3d syl ja bicomd elun rexbii syl6bbr
      eluni2 orbi2i bitri eliun 3bitr4g eqrdv wex wa eleq2 eluni ax-17 19.45
      3bitr4i df-rex 3bitr3g biimpd 19.39 orc pm3.26 imim12i 19.22i 3syl
      19.37v sylib 19.23adv neq0 3imtr4g con4d impbii ) CEFZBEFZGZBCHZIZACBAJZI
      ZKZFZWCDWEWHWCDJZBLZWJWFLZACMZNZWJWGLZACMZWJWELZWJWHLZWCWNWKWLNZACMZWPWCW
      TWNWAWBWTWNOZWAPZCEQXACERWKWLACSTWBWKPZXAWKWBBWJUAUBXCWMWTWNXCWLWSACWKWLU
      CUDWKWMUCUEUFUGUHWOWSACWJBWFUIUJUKWQWKWJWDLZNZWNWJBWDUIZXDWMWKAWJCULUMUNA
      WJCWGUOZUPUQWIWBWAWIWKDURWFCLZAURZWBPXBWIWKXIDWIWKXHGZAURZWKXIGWIWKWLXHUS
      ZNZAURZXHWOUSZAURZGXMXOGZAURXKWIXNXPWIWQWRXNXPWEWHWJUTXEWKXLAURZNWQXNXDXR
      WKAWJCVAUMXFWKXLAWKAVBVCVDWRWPXPXGWOACVEUNVFVGXMXOAVHXQXJAWKXMXOXHWKXLVIX
      HWOVJVKVLVMWKXHAVNVOVPDBVQACVQVRVSVT $.
      $( [25-Jul-2011] $) $( [25-Nov-2003] $)

    $( A relationship involving union and indexed union.  Exercise 25 of
       [Enderton] p. 33. $)
    iununiOLD $p |- ( ( B = (/) -> A = (/) ) <->
                ( A u. U. B ) = U_ x e. B ( A u. x ) ) $=
      ( vy c0 wceq wi cuni cun cv ciun wcel wrex wo wn wb imor wne df-ne
      r19.45zv sylbir n0i con2i biorf rexbidv bitr3d syl jaoi bicomd sylbi
      elun rexbii syl6bbr eluni2 orbi2i bitri eliun 3bitr4g eqrdv wex wa eleq2
      eluni ax-17 19.45 3bitr4i df-rex 3bitr3g biimpd 19.39 orc pm3.26 imim12i
      19.22i 3syl 19.37v sylib 19.23adv neq0 3imtr4g con4d impbii ) CEFZBEFZGZB
      CHZIZACBAJZIZKZFZWEDWGWJWEDJZBLZWLWHLZACMZNZWLWILZACMZWLWGLZWLWJLZWEWPWMW
      NNZACMZWRWEWCOZWDNZWPXBPWCWDQXDXBWPXCXBWPPZWDXCCERXECESWMWNACTUAWDWMOZXEW
      MWDBWLUBUCXFWOXBWPXFWNXAACWMWNUDUEWMWOUDUFUGUHUIUJWQXAACWLBWHUKULUMWSWMWL
      WFLZNZWPWLBWFUKZXGWOWMAWLCUNUOUPAWLCWIUQZURUSWKWDWCWKWMDUTWHCLZAUTZWDOXCW
      KWMXLDWKWMXKGZAUTZWMXLGWKWMWNXKVAZNZAUTZXKWQVAZAUTZGXPXRGZAUTXNWKXQXSWKWS
      WTXQXSWGWJWLVBXHWMXOAUTZNWSXQXGYAWMAWLCVCUOXIWMXOAWMAVDVEVFWTWRXSXJWQACVG
      UPVHVIXPXRAVJXTXMAWMXPXRXKWMXOVKXKWQVLVMVNVOWMXKAVPVQVRDBVSACVSVTWAWB $.
      $( [28-Nov-2003] $) $( [25-Nov-2003] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Subclass relationship for power class and union. $)
    sspwuni $p |- ( A C_ ~P B <-> U. A C_ B ) $=
      ( vx cv cpw wcel wral wss cuni visset elpw ralbii dfss3 unissb 3bitr4i )
      CDZBEZFZCAGPBHZCAGAQHAIBHRSCAPBCJKLCAQMCABNO $.
      $( [5-May-2009] $) $( [18-Jul-2006] $)

    $( Two ways to express a collection of subclasses. $)
    pwssb $p |- ( A C_ ~P B <-> A. x e. A x C_ B ) $=
      ( cpw wss cuni cv wral sspwuni unissb bitri ) BCDEBFCEAGCEABHBCIABCJK $.
      $( [9-Jan-2007] $) $( [19-Jul-2006] $)
  $}

  $( Relationship for power class and union. $)
  elpwuni $p |- ( B e. A -> ( A C_ ~P B <-> U. A = B ) ) $=
    ( wcel cuni wss wceq cpw unissel expcom eqimss impbid1 sspwuni syl5bb ) BAC
    ZADZBEZOBFZABGENPQPNQABHIOBJKABLM $.
    $( [13-May-2007] $) $( [18-Jul-2006] $)

  ${
    $d x y A $.
    $( The power class of an intersection in terms of indexed intersection.
       Exercise 24(a) of [Enderton] p. 33. $)
    iinpw $p |- ~P |^| A = |^|_ x e. A ~P x $=
      ( vy cint cpw cv ciin wss wcel wral ssint visset elpw ralbii bitr4i cvv
      wb eliin ax-mp 3bitr4i eqriv ) CBDZEZABAFZEZGZCFZUBHZUGUEIZABJZUGUCIUGUFI
      ZUHUGUDHZABJUJAUGBKUIULABUGUDCLZMNOUGUBUMMUGPIUKUJQUMAUGBUEPRSTUA $.
      $( [29-Nov-2003] $) $( [29-Nov-2003] $)

    $( Inclusion of an indexed union of a power class in the power class of the
       union of its index.  Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpwss $p |- U_ x e. A ~P x C_ ~P U. A $=
      ( vy cv cpw ciun cuni wss wrex wcel ssiun eliun visset elpw rexbii bitri
      uniiun sseq2i 3imtr4i ssriv ) CABADZEZFZBGZEZCDZUAHZABIZUFABUAFZHZUFUCJZU
      FUEJZABUAUFKUKUFUBJZABIUHAUFBUBLUMUGABUFUACMZNOPULUFUDHUJUFUDUNNUDUIUFABQ
      RPST $.
      $( [25-Nov-2003] $) $( [25-Nov-2003] $)
  $}

