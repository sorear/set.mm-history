$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                          Power classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare the symbol for power class. $)
  $c P~ $.  $( Calligraphic P $)

  $( Extend class notation to include power class.  (The tilde in the Metamath
     token is meant to suggest the calligraphic font of the P.) $)
  cpw $a class P~ A $.

  ${
    $d x A $.
    $( Define power class.  Definition 5.10 of [TakeutiZaring] p. 17,
       but we also let it apply to proper classes, i.e. those that are not
       members of ` V ` . $)
    df-pw $a |- P~ A = { x | x (_ A } $.
  $}

  ${
    $d x A $.  $d x B $.
    $( Equality theorem for the power class. $)
    pweq $p |- ( A = B -> P~ A = P~ B ) $=
      ( vx wceq cv wss cab cpw sseq2 abbidv df-pw 3eqtr4g ) ABDZCEZAFZCGNBFZCGA
      HBHMOPCABNIJCAKCBKL $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d A x $.  $d B x $.
    ${
      elpw.1 $e |- A e. V $.
      $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
      elpw $p |- ( A e. P~ B  <-> A (_ B ) $=
        ( vx cv cpw wcel wss eleq1 sseq1 df-pw abeq2i vtoclb ) DEZBFZGNBHZAOGAB
        HDACNAOINABJPDODBKLM $.
        $( [31-Dec-1993] $)
    $}

    $( Membership in a power class.  Theorem 86 of [Suppes] p. 47.  See also
       ~ elpw2g . $)
    elpwg $p |- ( A e. C -> ( A e. P~ B  <-> A (_ B ) ) $=
      ( vx cv cpw wcel wss wb wceq eleq1 sseq1 bibi12d visset elpw vtoclg ) DEZ
      BFZGZQBHZIARGZABHZIDACQAJSUATUBQARKQABLMQBDNOP $.
      $( [6-Aug-2000] $)
  $}

  $( Subset relation implied by membership in a power class. $)
  elpwi $p |- ( A e. P~ B -> A (_ B ) $=
    ( cpw wcel wss elpwg ibi ) ABCZDABEABHFG $.
    $( [20-Feb-2007] $) $( [17-Feb-2007] $)

  ${
    $d y z A $.  $d x y z $.
    hbpw.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for power class. $)
    hbpw $p |- ( y e. P~ A -> A. x y e. P~ A ) $=
      ( vz cv wss wal cpw wcel ax-17 hbel hbss visset elpw albii 3imtr4 ) BFZCG
      ZSAHRCIJZTAHAERCEFZRJAKABBUACRUAJAKDLMRCBNOZTSAUBPQ $.
      $( [6-Nov-2003] $) $( [28-Oct-2003] $)
  $}

  ${
    pwid.1 $e |- A e. V $.
    $( A set is a member of its power class.  Theorem 87 of [Suppes] p. 47. $)
    pwid $p |- A e. P~ A $=
      ( cpw wcel wss ssid elpw mpbir ) AACDAAEAFAABGH $.
      $( [5-Aug-1993] $)
  $}

