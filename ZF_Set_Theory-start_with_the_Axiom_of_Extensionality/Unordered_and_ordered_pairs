$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Unordered and ordered pairs
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols needed. $)
  $c <. $.  $( Bracket (the period distinguishes it from 'less than') $)
  $c >. $.  $( Bracket (the period distinguishes it from 'greater than') $)

  $( Extend class notation to include singleton. $)
  csn $a class { A } $.
  $( Extend class notation to include unordered pair. $)
  cpr $a class { A , B } $.
  $( Extend class notation to include ordered pair. $)
  cop $a class <. A , B >. $.

  ${
    $d x A $.  $d y A $.  $d z x $.  $d z y $.  $d z A $.
    $( Soundness justification theorem for ~ df-sn .  (Contributed by Rodolfo
       Medina, 28-Apr-2010.)  (The proof was shortened by Andrew Salmon,
       29-Jun-2011.) $)
    snjust $p |- { x | x = A } = { y | y = A } $=
      ( vz cv wceq cab eqeq1 cbvabv eqtri ) AEZCFZAGDEZCFZDGBEZCFZBGLNADKMCHINP
      DBMOCHIJ $.
      $( [29-Jun-2011] $) $( [28-Apr-2010] $)
  $}

  ${
    $d x A $.
    $( Define the singleton of a class.  Definition 7.1 of [Quine] p. 48.  For
       convenience, it is well-defined for proper classes, i.e., those that are
       not elements of ` _V ` , although it is not very meaningful in this
       case.  For an alternate definition see ~ dfsn2 . $)
    df-sn $a |- { A } = { x | x = A } $.
  $}

  $( Define unordered pair of classes.  Definition 7.1 of [Quine] p. 48.  For a
     more traditional definition, but requiring a dummy variable, see
     ~ dfpr2 . $)
  df-pr $a |- { A , B } = ( { A } u. { B } ) $.

  $( Extend class notation to include unordered triplet. $)
  ctp $a class { A , B , C } $.

  $( Define unordered triple of classes.  Definition of [Enderton] p. 19. $)
  df-tp $a |- { A , B , C } = ( { A , B } u. { C } ) $.

  $( Kuratowski's ordered pair definition.  Definition 9.1 of [Quine] p. 58.
     For proper classes it is not meaningful but is well-defined and we allow
     it for convenience (see ~ opprc1 , ~ opprc1b , ~ opprc2 , and ~ opprc3 ).
     For the justifying theorem (for sets) see ~ opth .  There are other ways
     to define ordered pairs; the basic requirement is that two ordered pairs
     are equal iff their respective members are equal.  In 1914 Norbert Wiener
     gave the first successful definition ` <. A , B >. ` _2
     ` = { { { A } , (/) } , { { B } } } ` , justified by ~ opthwiener , which
     was simplified by Kazimierz Kuratowski in 1921 to our present definition.
     An even simpler definition ` <. A , B >. ` _3 ` = { A , { A , B } } ` is
     justified by ~ opthreg , but it requires the Axiom of Regularity for its
     justification and is not commonly used.  A definition that also works for
     proper classes is ` <. A , B >. ` _4
     ` = ( ( A X. { (/) } ) u. ( B X. { { (/) } } ) ) ` , justified by
     ~ opthprc .  If we restrict our sets to nonnegative integers, an ordered
     pair definition that involves only elementary arithmetic is provided by
     ~ nn0opthi .  Finally, an ordered pair of real numbers can be represented
     by a complex number as shown by ~ crui . $)
  df-op $a |- <. A , B >. = { { A } , { A , B } } $.

  ${
    $d x A $.  $d x B $.
    $( Equality theorem for singletons.  Part of Exercise 4 of [TakeutiZaring]
       p. 15. $)
    sneq $p |- ( A = B -> { A } = { B } ) $=
      ( vx wceq cv cab csn eqeq2 abbidv df-sn 3eqtr4g ) ABDZCEZADZCFMBDZCFAGBGL
      NOCABMHICAJCBJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    sneqi.1 $e |- A = B $.
    $( Equality inference for singletons. $)
    sneqi $p |- { A } = { B } $=
      ( wceq csn sneq ax-mp ) ABDAEBEDCABFG $.
      $( [22-Jan-2004] $)
  $}

  ${
    sneqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for singletons. $)
    sneqd $p |- ( ph -> { A } = { B } ) $=
      ( wceq csn sneq syl ) ABCEBFCFEDBCGH $.
      $( [22-Jan-2004] $)
  $}

  $( Alternate definition of singleton.  Definition 5.1 of [TakeutiZaring]
     p. 15. $)
  dfsn2 $p |- { A } = { A , A } $=
    ( cpr csn cun df-pr unidm eqtr2i ) AABACZHDHAAEHFG $.
    $( [24-Apr-1994] $)

  ${
    $d x A $.
    $( There is only one element in a singleton.  Exercise 2 of [TakeutiZaring]
       p. 15. $)
    elsn $p |- ( x e. { A } <-> x = A ) $=
      ( cv wceq csn df-sn abeq2i ) ACBDABEABFG $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Alternate definition of unordered pair.  Definition 5.1 of
       [TakeutiZaring] p. 15. $)
    dfpr2 $p |- { A , B } = { x | ( x = A \/ x = B ) } $=
      ( cpr csn cun cv wceq wo cab df-pr wcel elun elsn orbi12i bitri abbi2i
      eqtri ) BCDBEZCEZFZAGZBHZUBCHZIZAJBCKUEAUAUBUALUBSLZUBTLZIUEUBSTMUFUCUGUD
      ABNACNOPQR $.
      $( [24-Apr-1994] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( A member of an unordered pair of classes is one or the other of them.
       Exercise 1 of [TakeutiZaring] p. 15, generalized. $)
    elprg $p |- ( A e. V -> ( A e. { B , C } <-> ( A = B \/ A = C ) ) ) $=
      ( vx cv wceq wo cpr eqeq1 orbi12d dfpr2 elab2g ) EFZBGZNCGZHABGZACGZHEABC
      IDNAGOQPRNABJNACJKEBCLM $.
      $( [13-Sep-1995] $)
  $}

  ${
    elpr.1 $e |- A e. _V $.
    $( A member of an unordered pair of classes is one or the other of them.
       Exercise 1 of [TakeutiZaring] p. 15. $)
    elpr $p |- ( A e. { B , C } <-> ( A = B \/ A = C ) ) $=
      ( cvv wcel cpr wceq wo wb elprg ax-mp ) AEFABCGFABHACHIJDABCEKL $.
      $( [13-Sep-1995] $)
  $}

  ${
    elpr2.1 $e |- B e. _V $.
    elpr2.2 $e |- C e. _V $.
    $( A member of an unordered pair of classes is one or the other of them.
       Exercise 1 of [TakeutiZaring] p. 15. $)
    elpr2 $p |- ( A e. { B , C } <-> ( A = B \/ A = C ) ) $=
      ( cpr wcel wceq wo elprg ibi cvv wb eleq1 mpbiri jaoi syl ibir impbii )
      ABCFZGZABHZACHZIZUAUDABCTJKUDUAUDALGZUAUDMUBUEUCUBUEBLGDABLNOUCUECLGEACLN
      OPABCLJQRS $.
      $( [14-Oct-2005] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( There is only one element in a singleton.  Exercise 2 of [TakeutiZaring]
       p. 15 (generalized).  (The proof was shortened by Andrew Salmon,
       29-Jun-2011.) $)
    elsncg $p |- ( A e. V -> ( A e. { B } <-> A = B ) ) $=
      ( vx cv wceq csn eqeq1 df-sn elab2g ) DEZBFABFDABGCKABHDBIJ $.
      $( [29-Jun-2011] $) $( [13-Sep-1995] $)
  $}

  ${
    elsnc.1 $e |- A e. _V $.
    $( There is only one element in a singleton.  Exercise 2 of [TakeutiZaring]
       p. 15. $)
    elsnc $p |- ( A e. { B } <-> A = B ) $=
      ( cvv wcel csn wceq wb elsncg ax-mp ) ADEABFEABGHCABDIJ $.
      $( [13-Sep-1995] $)
  $}

  $( There is only one element in a singleton. $)
  elsni $p |- ( A e. { B } -> A = B ) $=
    ( csn wcel wceq elsncg ibi ) ABCZDABEABHFG $.
    $( [5-Jun-1994] $)

  $( A set is a member of its singleton.  Part of Theorem 7.6 of [Quine]
     p. 49. $)
  snidg $p |- ( A e. V -> A e. { A } ) $=
    ( wcel csn wceq eqid elsncg mpbiri ) ABCAADCAAEAFAABGH $.
    $( [28-Oct-2003] $)

  $( A class is a set iff it is a member of its singleton. $)
  snidb $p |- ( A e. _V <-> A e. { A } ) $=
    ( cvv wcel csn snidg elisset impbii ) ABCAADZCABEAHFG $.
    $( [5-Apr-2004] $)

  ${
    snid.1 $e |- A e. _V $.
    $( A set is a member of its singleton.  Part of Theorem 7.6 of [Quine]
       p. 49. $)
    snid $p |- A e. { A } $=
      ( cvv wcel csn snidb mpbi ) ACDAAEDBAFG $.
      $( [31-Dec-1993] $)
  $}

  $( There is only one element in a singleton.  Exercise 2 of [TakeutiZaring]
     p. 15.  This variation requires only that ` B ` , rather than ` A ` , be a
     set. $)
  elsnc2g $p |- ( B e. V -> ( A e. { B } <-> A = B ) ) $=
    ( wcel csn wceq elsni snidg eleq1 syl5ibrcom impbid2 ) BCDZABEZDZABFZABGLNO
    BMDBCHABMIJK $.
    $( [28-Oct-2003] $)

  ${
    elsnc2.1 $e |- B e. _V $.
    $( There is only one element in a singleton.  Exercise 2 of [TakeutiZaring]
       p. 15.  This variation requires only that ` B ` , rather than ` A ` , be
       a set. $)
    elsnc2 $p |- ( A e. { B } <-> A = B ) $=
      ( cvv wcel csn wceq wb elsnc2g ax-mp ) BDEABFEABGHCABDIJ $.
      $( [12-Jun-1994] $)
  $}

  ${
    $d A x $.  $d ps x $.
    rexsng.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted existential quantification over a singleton. $)
    rexsng $p |- ( A e. V -> ( E. x e. { A } ph <-> ps ) ) $=
      ( csn wrex cv wceq wa wex wcel df-rex elsn anbi1i exbii bitri ceqsexgv
      syl5bb ) ACDGZHZCIZDJZAKZCLZDEMBUBUCUAMZAKZCLUFACUANUHUECUGUDACDOPQRABCDE
      FST $.
      $( [29-Jan-2012] $)
  $}

  ${
    $d A x $.  $d ps x $.
    rexsn.1 $e |- A e. _V $.
    rexsn.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Restricted existential quantification over a singleton.  (Contributed by
       Jeff Madsen, 5-Jan-2011.) $)
    rexsn $p |- ( E. x e. { A } ph <-> ps ) $=
      ( cvv wcel csn wrex wb rexsng ax-mp ) DGHACDIJBKEABCDGFLM $.
      $( [5-Jan-2011] $)
  $}

  ${
    eltp.1 $e |- A e. _V $.
    $( A member of an unordered triple of classes is one of them.  Special case
       of Exercise 1 of [TakeutiZaring] p. 17. $)
    eltp $p |- ( A e. { B , C , D } <-> ( A = B \/ A = C \/ A = D ) ) $=
      ( ctp wcel wceq wo w3o cpr csn cun df-tp eleq2i elun elpr elsnc orbi12i
      3bitri df-3or bitr4i ) ABCDFZGZABHZACHZIZADHZIZUEUFUHJUDABCKZDLZMZGAUJGZA
      UKGZIUIUCULABCDNOAUJUKPUMUGUNUHABCEQADERSTUEUFUHUAUB $.
      $( [8-Apr-1994] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Alternate definition of unordered triple of classes.  Special case of
       Definition 5.3 of [TakeutiZaring] p. 16. $)
    dftp2 $p |- { A , B , C } = { x | ( x = A \/ x = B \/ x = C ) } $=
      ( cv wceq w3o ctp visset eltp abbi2i ) AEZBFLCFLDFGABCDHLBCDAIJK $.
      $( [8-Apr-1994] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbpr.1 $e |- ( y e. A -> A. x y e. A ) $.
    hppr.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for unordered pairs. $)
    hbpr $p |- ( y e. { A , B } -> A. x y e. { A , B } ) $=
      ( cv wceq wo wal cpr wcel hbeleq hbor visset elpr albii 3imtr4i ) BGZCHZS
      DHZIZUBAJSCDKLZUCAJTUAAABCEMABDFMNSCDBOPZUCUBAUDQR $.
      $( [14-Nov-1995] $)
  $}

  $( Membership of a conditional operator in an unordered pair. $)
  ifpr $p |- ( ( A e. C /\ B e. D ) -> if ( ph , A , B ) e. { A , B } ) $=
    ( wcel cvv cif cpr elisset wa ifcl wceq wo ifor elprg mpbiri syl syl2an )
    BDFBGFZCGFZABCHZBCIFZCEFBDJCEJTUAKUBGFZUCABCGLUDUCUBBMUBCMNABCOUBBCGPQRS $.
    $( [17-Jun-2007] $)

  ${
    $d x A $.  $d x B $.
    $( Convert a quantification over a pair to a conjunction. $)
    ralprg $p |- ( ( A e. V /\ B e. W ) -> ( A. x e. { A , B } ph <->
                ( [ A / x ] ph /\ [ B / x ] ph ) ) ) $=
      ( wcel wa wsbc cv wceq wi wal cpr wral sbc6g bi2anan9 df-ral wo visset
      elpr imbi1i jaob bitri albii 19.26 3bitri syl6rbbr ) CEGZDFGZHABCIZABDIZH
      BJZCKZALZBMZUMDKZALZBMZHZABCDNZOZUIUKUPUJULUSABCEPABDFPQVBUMVAGZALZBMUOUR
      HZBMUTABVARVDVEBVDUNUQSZALVEVCVFAUMCDBTUAUBUNAUQUCUDUEUOURBUFUGUH $.
      $( [17-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    ralpr.1 $e |- A e. _V $.
    ralpr.2 $e |- B e. _V $.
    $( Convert a quantification over a pair to a conjunction. $)
    ralpr $p |- ( A. x e. { A , B } ph <->
                ( [ A / x ] ph /\ [ B / x ] ph ) ) $=
      ( cvv wcel cpr wral wsbc wa wb ralprg mp2an ) CGHDGHABCDIJABCKABDKLMEFABC
      DGGNO $.
      $( [18-Sep-2011] $) $( [3-Jun-2007] $)

    $( Convert an existential quantification over a pair to a disjunction. $)
    rexpr $p |- ( E. x e. { A , B } ph <->
                ( [ A / x ] ph \/ [ B / x ] ph ) ) $=
      ( wn cpr wral wsbc wa wrex wo ralpr cvv wcel wb sbcng ax-mp anbi12i
      bitri notbii dfrex2 oran 3bitr4i ) AGZBCDHZIZGABCJZGZABDJZGZKZGABUGLUIUKM
      UHUMUHUFBCJZUFBDJZKUMUFBCDEFNUNUJUOULCOPUNUJQEABCORSDOPUOULQFABDORSTUAUBA
      BUGUCUIUKUDUE $.
      $( [3-Jun-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    raltp.1 $e |- A e. _V $.
    raltp.2 $e |- B e. _V $.
    raltp.3 $e |- C e. _V $.
    $( Convert a quantification over a triple to a conjunction. $)
    raltp $p |- ( A. x e. { A , B , C } ph <->
                ( [ A / x ] ph /\ [ B / x ] ph /\ [ C / x ] ph ) ) $=
      ( cv ctp wcel wi wal wceq w3a wral wsbc w3o visset eltp imbi1i 3jaob
      bitri albii 19.26-3an df-ral sbc6 3anbi123i 3bitr4i ) BIZCDEJZKZALZBMZUJC
      NZALZBMZUJDNZALZBMZUJENZALZBMZOZABUKPABCQZABDQZABEQZOUNUPUSVBOZBMVDUMVHBU
      MUOURVARZALVHULVIAUJCDEBSTUAUOAURVAUBUCUDUPUSVBBUEUCABUKUFVEUQVFUTVGVCABC
      FUGABDGUGABEHUGUHUI $.
      $( [13-Sep-2011] $)
  $}

  ${
    $d x A $.
    ralsn.1 $e |- A e. _V $.
    $( MAY BE REVISED - derive from ralsng? $)
    $( Convert a quantification over a singleton to a substitution. $)
    ralsn $p |- ( A. x e. { A } ph <-> [ A / x ] ph ) $=
      ( csn wral cpr wsbc wa dfsn2 raleqi ralpr anidm 3bitri ) ABCEZFABCCGZFABC
      HZQIQABOPCJKABCCDDLQMN $.
      $( [27-Apr-2009] $)
  $}

  ${
    $d x A $.
    $( Substitution expressed in terms of quantification over a singleton. $)
    ralsng $p |- ( A e. V -> ( A. x e. { A } ph <-> [ A / x ] ph ) ) $=
      ( wcel wsbc cv wceq wi wal csn wral sbc6g df-ral elsn imbi1i albii bitri
      syl6rbbr ) CDEABCFBGZCHZAIZBJZABCKZLZABCDMUETUDEZAIZBJUCABUDNUGUBBUFUAABC
      OPQRS $.
      $( [14-May-2009] $) $( [14-Dec-2005] $)

    $( MAY BE REVISED - make obsolete; use ralsng instead - also,
       shorten posn w/ ralsn or ralsng $)
    $( Substitution expressed in terms of quantification over a singleton.
       (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    sbcsng $p |- ( A e. V -> ( [ A / x ] ph <-> A. x e. { A } ph ) ) $=
      ( wcel csn wral wsbc ralsng bicomd ) CDEABCFGABCHABCDIJ $.
      $( [29-Jun-2011] $) $( [14-Dec-2005] $)
  $}

  ${
    $d y A $.  $d x y $.
    hbsn.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for singletons. $)
    hbsn $p |- ( y e. { A } -> A. x y e. { A } ) $=
      ( csn cpr dfsn2 hbpr hbxfr ) ABCECCFCGABCCDDHI $.
      $( [14-Nov-1995] $)
  $}

  ${
    $d A y $.  $d B y $.  $d V y $.  $d x y $.
    $( Distribute proper substitution through the singleton of a class.
       ~ csbsng is derived from the virtual deduction proof ~ csbsngVD .
       (Contributed by Alan Sare, 10-Nov-2012. ) $)
    csbsng $p |- ( A e. V -> [_ A / x ]_ { B } = { [_ A / x ]_ B } ) $=
      ( vy wcel csn csb cv wceq cab wsbc df-sn csbeq2i csbabg sbceq2g abbidv
      3eqtrd syl6eqr ) BDFZABCGZHZEIZABCHZJZEKZUDGTUBABUCCJZEKZHUGABLZEKUFABUAU
      HDECMNUGAEBDOTUIUEEABUCCDPQREUDMS $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Intersection with the singleton of a non-member is disjoint.  (The proof
       was shortened by Andrew Salmon, 29-Jun-2011.) $)
    disjsn $p |- ( ( A i^i { B } ) = (/) <-> -. B e. A ) $=
      ( vx cv wcel csn wn wi wal wceq wa wex cin c0 con2b elsn imbi1i imnan
      3bitri albii alnex bitri disj1 df-clel notbii 3bitr4i ) CDZAEZUGBFZEZGHZC
      IZUGBJZUHKZCLZGZAUIMNJBAEZGULUNGZCIUPUKURCUKUJUHGZHUMUSHURUHUJOUJUMUSCBPQ
      UMUHRSTUNCUAUBCAUIUCUQUOCBAUDUEUF $.
      $( [29-Jun-2011] $) $( [22-May-1998] $)
  $}

  $( Intersection of distinct singletons is disjoint. $)
  disjsn2 $p |- ( A =/= B -> ( { A } i^i { B } ) = (/) ) $=
    ( wne csn wcel wn cin c0 wceq elsni eqcomd necon3ai disjsn sylibr ) ABCBADZ
    EZFOBDGHIPABPBABAJKLOBMN $.
    $( [2-Jun-2007] $) $( [25-May-1998] $)

  ${
    $d x A $.
    $( The singleton of a proper class (one that doesn't exist) is the empty
       set.  Theorem 7.2 of [Quine] p. 48. $)
    snprc $p |- ( -. A e. _V <-> { A } = (/) ) $=
      ( vx csn c0 wceq cvv wcel cv wex wn elsn exbii neq0 isset 3bitr4i
      con1bii ) ACZDEZAFGZBHZQGZBITAEZBIRJSUAUBBBAKLBQMBANOP $.
      $( [5-Aug-1993] $)
  $}

  ${
    $d x y A $.  $d x B $.
    r19.12sn.1 $e |- A e. _V $.
    $( Special case of ~ r19.12 where its converse holds. $)
    r19.12sn $p |- ( E. x e. { A } A. y e. B ph
                <-> A. y e. B E. x e. { A } ph ) $=
      ( wral cpr wrex wsbc csn wo rexpr oridm cvv wcel wb sbcralg ax-mp 3bitri
      wceq dfsn2 rexeq ralbii 3bitr4i ) ACEGZBDDHZIZABDJZCEGZUFBDKZIZABUKIZCEGU
      HUFBDJZUNLUNUJUFBDDFFMUNNDOPUNUJQFABCDEORSTUKUGUAZULUHQDUBZUFBUKUGUCSUMUI
      CEUMABUGIZUIUILUIUOUMUQQUPABUKUGUCSABDDFFMUINTUDUE $.
      $( [19-May-2008] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Condition where a restricted class abstraction is a singleton. $)
    rabsn $p |- ( B e. A -> { x e. A | x = B } = { B } ) $=
      ( wcel cv wceq wa cab crab csn eleq1 pm5.32ri baib abbidv df-rab df-sn
      3eqtr4g ) CBDZAEZBDZSCFZGZAHUAAHUAABICJRUBUAAUBRUAUATRSCBKLMNUAABOACPQ $.
      $( [28-May-2006] $)
  $}

  ${
    $d x y z $.  $d y ph $.
    $( Another way to express existential uniqueness of a wff: its class
       abstraction is a singleton. $)
    euabsn $p |- ( E! x ph <-> E. x { x | ph } = { x } ) $=
      ( vy vz cab cv csn wceq wex wb wal weu wcel abeq1 elsn bibi2i albii
      bitri exbii ax-17 hbab1 hbeq sneq eqeq2d cbvex df-eu 3bitr4ri ) ABEZCFZGZ
      HZCIABFZUIHZJZBKZCIUHULGZHZBIABLUKUOCUKAULUJMZJZBKUOABUJNUSUNBURUMABUIOPQ
      RSUQUKBCUQCTBCDUHUJABCUADFUJMBTUBUMUPUJUHULUIUCUDUEABCUFUG $.
      $( [22-Feb-2004] $)
  $}

  ${
    $d x A $.
    $( Two ways to express " ` A ` is a singleton." $)
    eusn $p |- ( E! x x e. A <-> E. x A = { x } ) $=
      ( cv wcel weu cab csn wceq wex euabsn abid2 eqeq1i exbii bitri ) ACZBDZAE
      PAFZOGZHZAIBRHZAIPAJSTAQBRABKLMN $.
      $( [30-Oct-2010] $)
  $}

  $( Commutative law for unordered pairs. $)
  prcom $p |- { A , B } = { B , A } $=
    ( csn cun cpr uncom df-pr 3eqtr4i ) ACZBCZDJIDABEBAEIJFABGBAGH $.
    $( [5-Aug-1993] $)

  $( Equality theorem for unordered pairs. $)
  preq1 $p |- ( A = B -> { A , C } = { B , C } ) $=
    ( wceq csn cun cpr sneq uneq1d df-pr 3eqtr4g ) ABDZAEZCEZFBEZNFACGBCGLMONAB
    HIACJBCJK $.
    $( [29-Mar-1998] $)

  $( Equality theorem for unordered pairs. $)
  preq2 $p |- ( A = B -> { C , A } = { C , B } ) $=
    ( wceq cpr preq1 prcom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [5-Aug-1993] $)

  $( Equality theorem for unordered pairs. $)
  preq12 $p |- ( ( A = C /\ B = D ) -> { A , B } = { C , D } ) $=
    ( wceq cpr preq1 preq2 sylan9eq ) ACEBDEABFCBFCDFACBGBDCHI $.
    $( [19-Oct-2012] $)

  ${
    preq1i.1 $e |- A = B $.
    $( Equality inference for unordered pairs. $)
    preq1i $p |- { A , C } = { B , C } $=
      ( wceq cpr preq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [19-Oct-2012] $)

    $( Equality inference for unordered pairs. $)
    preq2i $p |- { C , A } = { C , B } $=
      ( wceq cpr preq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [19-Oct-2012] $)

    ${
      preq12i.2 $e |- C = D $.
      $( Equality inference for unordered pairs. $)
      preq12i $p |- { A , C } = { B , D } $=
        ( wceq cpr preq12 mp2an ) ABGCDGACHBDHGEFACBDIJ $.
        $( [19-Oct-2012] $)
    $}
  $}

  ${
    preq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for unordered pairs. $)
    preq1d $p |- ( ph -> { A , C } = { B , C } ) $=
      ( wceq cpr preq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [19-Oct-2012] $)

    $( Equality deduction for unordered pairs. $)
    preq2d $p |- ( ph -> { C , A } = { C , B } ) $=
      ( wceq cpr preq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [19-Oct-2012] $)

    ${
      preq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for unordered pairs. $)
      preq12d $p |- ( ph -> { A , C } = { B , D } ) $=
        ( wceq cpr preq12 syl11anc ) ABCHDEHBDICEIHFGBDCEJK $.
        $( [19-Oct-2012] $)
    $}
  $}

  $( Equality theorem for unordered triples. $)
  tpeq1 $p |- ( A = B -> { A , C , D } = { B , C , D } ) $=
    ( wceq cpr csn cun ctp preq1 uneq1d df-tp 3eqtr4g ) ABEZACFZDGZHBCFZPHACDIB
    CDINOQPABCJKACDLBCDLM $.
    $( [13-Sep-2011] $)

  $( Equality theorem for unordered triples. $)
  tpeq2 $p |- ( A = B -> { C , A , D } = { C , B , D } ) $=
    ( wceq cpr csn cun ctp preq2 uneq1d df-tp 3eqtr4g ) ABEZCAFZDGZHCBFZPHCADIC
    BDINOQPABCJKCADLCBDLM $.
    $( [13-Sep-2011] $)

  $( Equality theorem for unordered triples. $)
  tpeq3 $p |- ( A = B -> { C , D , A } = { C , D , B } ) $=
    ( wceq cpr csn cun ctp sneq uneq2d df-tp 3eqtr4g ) ABEZCDFZAGZHOBGZHCDAICDB
    INPQOABJKCDALCDBLM $.
    $( [13-Sep-2011] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Rotation of the elements of an unordered triple.  (Contributed by Alan
       Sare, 24-Oct-2011.) $)
    tprot $p |- { A , B , C } = { B , C , A } $=
      ( vx ctp cv wceq w3o cab dftp2 3orrot abbii eqtri eqtr4i ) ABCEZDFZBGZPCG
      ZPAGZHZDIZBCAEOSQRHZDIUADABCJUBTDSQRKLMDBCAJN $.
      $( [24-Oct-2011] $)
  $}

  $( An unordered pair contains its first member.  Part of Theorem 7.6 of
     [Quine] p. 49.  (Contributed by Stefan Allan, 8-Nov-2008.) $)
  prid1g $p |- ( A e. V -> A e. { A , B } ) $=
    ( wcel cpr wceq wo eqid orci elprg mpbiri ) ACDAABEDAAFZABFZGLMAHIAABCJK $.
    $( [7-Nov-2008] $)

  $( An unordered pair contains its second member.  Part of Theorem 7.6 of
     [Quine] p. 49.  (Contributed by Stefan Allan, 8-Nov-2008.) $)
  prid2g $p |- ( B e. V -> B e. { A , B } ) $=
    ( wcel cpr prid1g prcom syl6eleq ) BCDBBAEABEBACFBAGH $.
    $( [7-Nov-2008] $)

  ${
    prid1.1 $e |- A e. _V $.
    $( An unordered pair contains its first member.  Part of Theorem 7.6 of
       [Quine] p. 49. $)
    prid1 $p |- A e. { A , B } $=
      ( cvv wcel cpr prid1g ax-mp ) ADEAABFECABDGH $.
      $( [5-Aug-1993] $)
  $}

  ${
    prid2.1 $e |- B e. _V $.
    $( An unordered pair contains its second member.  Part of Theorem 7.6 of
       [Quine] p. 49. $)
    prid2 $p |- B e. { A , B } $=
      ( cpr prid1 prcom eleqtri ) BBADABDBACEBAFG $.
      $( [5-Aug-1993] $)
  $}

  $( A proper class vanishes in an unordered pair. $)
  prprc1 $p |- ( -. A e. _V -> { A , B } = { B } ) $=
    ( cvv wcel wn csn c0 wceq cpr snprc cun uneq1 df-pr uncom un0 eqtr2i
    3eqtr4g sylbi ) ACDEAFZGHZABIZBFZHAJTSUBKGUBKZUAUBSGUBLABMUCUBGKUBGUBNUBOPQ
    R $.
    $( [5-Aug-1993] $)

  $( A proper class vanishes in an unordered pair. $)
  prprc2 $p |- ( -. B e. _V -> { A , B } = { A } ) $=
    ( cvv wcel wn cpr csn prcom prprc1 syl5eq ) BCDEABFBAFAGABHBAIJ $.
    $( [22-Mar-2006] $)

  $( An unordered pair containing two proper classes is the empty set. $)
  prprc $p |- ( ( -. A e. _V /\ -. B e. _V ) -> { A , B } = (/) ) $=
    ( cvv wcel wn cpr csn c0 prprc1 wceq snprc biimpi sylan9eq ) ACDEBCDEZABFBG
    ZHABINOHJBKLM $.
    $( [22-Mar-2006] $)

  ${
    tpid1.1 $e |- A e. _V $.
    $( One of the three elements of an unordered triple.  (The proof was
       shortened by Andrew Salmon, 29-Jun-2011.) $)
    tpid1 $p |- A e. { A , B , C } $=
      ( ctp wcel wceq w3o eqid 3mix1 ax-mp eltp mpbir ) AABCEFAAGZABGZACGZHZNQA
      INOPJKAABCDLM $.
      $( [29-Jun-2011] $) $( [7-Apr-1994] $)
  $}

  ${
    tpid2.1 $e |- B e. _V $.
    $( One of the three elements of an unordered triple.  (The proof was
       shortened by Andrew Salmon, 29-Jun-2011.) $)
    tpid2 $p |- B e. { A , B , C } $=
      ( ctp wcel wceq w3o eqid 3mix2 ax-mp eltp mpbir ) BABCEFBAGZBBGZBCGZHZOQB
      IONPJKBABCDLM $.
      $( [29-Jun-2011] $) $( [7-Apr-1994] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.  $d x ps $.
    $( Closed theorem form of ~ tpid3 .  This proof was automatically generated
       from the virtual deduction proof ~ tpid3gVD using a translation
       program.  (Contributed by Alan Sare, 24-Oct-2011.) $)
    tpid3g $p |- ( A e. B -> A e. { C , D , A } ) $=
      ( vx wcel cv wceq wex ctp elex wi wal w3o cab 3mix3 a1i abid syl6ibr
      dftp2 eleq2i eleq1 mpbidi 19.21aiv 19.23v sylib mpd ) ABFZEGZAHZEIZACDAJZ
      FZEABKUHUJUMLZEMUKUMLUHUNEUJUIULFZUMUHUHUJUIUICHZUIDHZUJNZEOZFZUOUHUJURUT
      UJURLUHUJUPUQPQURERSULUSUIECDATUASUIAULUBUCUDUJUMEUEUFUG $.
      $( [24-Oct-2011] $)
  $}

  ${
    tpid3.1 $e |- C e. _V $.
    $( One of the three elements of an unordered triple.  (The proof was
       shortened by Andrew Salmon, 29-Jun-2011.) $)
    tpid3 $p |- C e. { A , B , C } $=
      ( ctp wcel wceq w3o eqid 3mix3 ax-mp eltp mpbir ) CABCEFCAGZCBGZCCGZHZPQC
      IPNOJKCABCDLM $.
      $( [29-Jun-2011] $) $( [7-Apr-1994] $)
  $}

  $( The singleton of a set is not empty. $)
  snnzg $p |- ( A e. V -> { A } =/= (/) ) $=
    ( wcel csn c0 wne snidg ne0i syl ) ABCAADZCJEFABGJAHI $.
    $( [14-Dec-2008] $)

  ${
    snnz.1 $e |- A e. _V $.
    $( The singleton of a set is not empty. $)
    snnz $p |- { A } =/= (/) $=
      ( cvv wcel csn c0 wne snnzg ax-mp ) ACDAEFGBACHI $.
      $( [2-Apr-2007] $) $( [10-Apr-1994] $)
  $}

  ${
    prnz.1 $e |- A e. _V $.
    $( A pair containing a set is not empty. $)
    prnz $p |- { A , B } =/= (/) $=
      ( cpr wcel c0 wne prid1 ne0i ax-mp ) AABDZEKFGABCHKAIJ $.
      $( [17-Jan-2007] $) $( [9-Apr-1994] $)
  $}

  ${
    tpnz.1 $e |- A e. _V $.
    $( A triplet containing a set is not empty. $)
    tpnz $p |- { A , B , C } =/= (/) $=
      ( ctp wcel c0 wne tpid1 ne0i ax-mp ) AABCEZFLGHABCDILAJK $.
      $( [13-Apr-2007] $) $( [10-Apr-1994] $)
  $}

  ${
    $d A x $.  $d B x $.
    snss.1 $e |- A e. _V $.
    $( The singleton of an element of a class is a subset of the class.
       Theorem 7.4 of [Quine] p. 49. $)
    snss $p |- ( A e. B <-> { A } C_ B ) $=
      ( vx cv csn wcel wi wal wceq wss elsn imbi1i albii dfss2 clel2 3bitr4ri
      ) DEZAFZGZRBGZHZDIRAJZUAHZDISBKABGUBUDDTUCUADALMNDSBODABCPQ $.
      $( [5-Aug-1993] $)
  $}

  $( Membership in a set with an element removed. $)
  eldifsn $p |- ( A e. ( B \ { C } ) <-> ( A e. B /\ A =/= C ) ) $=
    ( csn cdif wcel wn wa wne eldif elsncg necon3bbid pm5.32i bitri ) ABCDZEFAB
    FZAOFZGZHPACIZHABOJPRSPQACACBKLMN $.
    $( [10-Oct-2007] $)

  ${
    $d A x $.  $d B x $.
    $( The singleton of an element of a class is a subset of the class.
       Theorem 7.4 of [Quine] p. 49. $)
    snssg $p |- ( A e. V -> ( A e. B <-> { A } C_ B ) ) $=
      ( vx cv wcel csn wss eleq1 wceq sneq sseq1d visset snss vtoclbg ) DEZBFPG
      ZBHABFAGZBHDACPABIPAJQRBPAKLPBDMNO $.
      $( [22-Jul-2001] $)

    $( An element not in a set can be removed without affecting the set.  (The
       proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    difsn $p |- ( -. A e. B -> ( B \ { A } ) = B ) $=
      ( vx wcel wn csn cdif cv wne wa eldifsn simpl wceq eleq1 biimpcd
      necon3bd com12 ancld impbid2 syl5bb eqrdv ) ABDZEZCBAFGZBCHZUDDUEBDZUEAIZ
      JZUCUFUEBAKUCUHUFUFUGLUCUFUGUFUCUGUFUBUEAUEAMUFUBUEABNOPQRSTUA $.
      $( [29-Jun-2011] $) $( [16-Mar-2006] $)

    $( Removal of a singleton from an unordered pair.  (The proof was shortened
       by Andrew Salmon, 29-Jun-2011.) $)
    difprsn $p |- ( { A , B } \ { A } ) C_ { B } $=
      ( vx cpr csn cdif cv wcel wn wa wceq wo visset elpr elsn notbii anbi12i
      biorf biimparc sylbi eldif 3imtr4i ssriv ) CABDZAEZFZBEZCGZUDHZUHUEHZIZJZ
      UHBKZUHUFHUHUGHULUHAKZUMLZUNIZJUMUIUOUKUPUHABCMNUJUNCAOPQUPUMUOUNUMRSTUHU
      DUEUACBOUBUC $.
      $( [29-Jun-2011] $) $( [16-Mar-2006] $)
  $}

  $( The singleton of an element of a class is a subset of the class. $)
  snssi $p |- ( A e. B -> { A } C_ B ) $=
    ( wcel csn wss snssg ibi ) ABCADBEABBFG $.
    $( [6-Jun-1994] $)

  ${
    snssd.1 $e |- ( ph -> A e. B ) $.
    $( First-order logic and set theory.  (Contributed by Jonathan Ben-Naim
       12-Jun-2011). $)
    snssd $p |- ( ph -> { A } C_ B ) $=
      ( wcel csn wss wb snssg syl mpbid ) ABCEZBFCGZDALLMHDBCCIJK $.
      $( [12-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  $( If we remove a single element from a class then put it back in, we end up
     with the original class. $)
  difsnid $p |- ( B e. A -> ( ( A \ { B } ) u. { B } ) = A ) $=
    ( wcel csn cdif cun uncom wss wceq snssi undif sylib syl5eq ) BACZABDZEZOFO
    PFZAPOGNOAHQAIBAJOAKLM $.
    $( [2-Oct-2006] $)

  $( Note that ` x ` is a dummy variable in the proof below. $)
  $( Compute the power set of the empty set.  Theorem 89 of [Suppes] p. 47.
     (The proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
  pw0 $p |- ~P (/) = { (/) } $=
    ( vx cv c0 wss cab wceq cpw csn ss0b abbii df-pw df-sn 3eqtr4i ) ABZCDZAENC
    FZAECGCHOPANIJACKACLM $.
    $( [29-Jun-2011] $) $( [5-Aug-1993] $)

  ${
    $d x y $.
    $( Compute the power set of the power set of the empty set.  (See ~ pw0 for
       the power set of the empty set.)  Theorem 90 of [Suppes] p. 48.
       Although this theorem is a special case of ~ pwsn , we have chosen to
       show a direct elementary proof. $)
    pwpw0 $p |- ~P { (/) } = { (/) , { (/) } } $=
      ( vx vy cv c0 csn wss cab wceq wo cpw cpr wn wa wcel wi wal dfss2 elsn
      imbi2i albii bitri wex neq0 exintr syl5bi exancom df-clel bitr4i snssi
      sylbi syl6 anc2li eqss syl6ibr orrd 0ss sseq1 mpbiri eqimss jaoi impbii
      abbii df-pw dfpr2 3eqtr4i ) ACZDEZFZAGVFDHZVFVGHZIZAGVGJDVGKVHVKAVHVKVHVI
      VJVHVILZVHVGVFFZMVJVHVLVMVHBCZVFNZVNDHZOZBPZVLVMOVHVOVNVGNZOZBPVRBVFVGQVT
      VQBVSVPVOBDRSTUAVRVLVOVPMBUBZVMVLVOBUBVRWABVFUCVOVPBUDUEWADVFNZVMWAVPVOMB
      UBWBVOVPBUFBDVFUGUHDVFUIUJUKUJULVFVGUMUNUOVIVHVJVIVHDVGFVGUPVFDVGUQURVFVG
      USUTVAVBAVGVCADVGVDVE $.
      $( [28-Sep-2006] $) $( [7-Aug-1994] $)
  $}

  $( A singleton is a subset of an unordered pair containing its member.  (The
     proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
  snsspr1 $p |- { A } C_ { A , B } $=
    ( csn cpr wss cun ssun1 df-pr sseq2i mpbir ) ACZABDZEKKBCZFZEKMGLNKABHIJ $.
    $( [29-Jun-2011] $) $( [27-Aug-2004] $)

  $( A singleton is a subset of an unordered pair containing its member. $)
  snsspr2 $p |- { B } C_ { A , B } $=
    ( csn cpr snsspr1 prcom sseqtri ) BCBADABDBAEBAFG $.
    $( [2-May-2009] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    prss.1 $e |- A e. _V $.
    prss.2 $e |- B e. _V $.
    $( A pair of elements of a class is a subset of the class.  Theorem 7.5 of
       [Quine] p. 49.  (The proof was shortened by Andrew Salmon,
       29-Jun-2011.) $)
    prss $p |- ( ( A e. C /\ B e. C ) <-> { A , B } C_ C ) $=
      ( csn wss wa cun wcel cpr unss snss anbi12i df-pr sseq1i 3bitr4i ) AFZCGZ
      BFZCGZHRTIZCGACJZBCJZHABKZCGRTCLUCSUDUAACDMBCEMNUEUBCABOPQ $.
      $( [29-Jun-2011] $) $( [30-May-1994] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.
    $( A pair of elements of a class is a subset of the class.  Theorem 7.5 of
       [Quine] p. 49.  (The proof was shortened by Andrew Salmon,
       29-Jun-2011.) $)
    prssg $p |- ( ( A e. V /\ B e. W ) ->
                ( ( A e. C /\ B e. C ) <-> { A , B } C_ C ) ) $=
      ( wcel wa csn wss cpr snssg bi2anan9 cun unss df-pr sseq1i bitr4i syl6bb
      ) ADFZBEFZGACFZBCFZGAHZCIZBHZCIZGZABJZCIZSUAUDTUBUFACDKBCEKLUGUCUEMZCIUIU
      CUECNUHUJCABOPQR $.
      $( [29-Jun-2011] $) $( [22-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The subsets of a singleton. $)
    sssn $p |- ( A C_ { B } <-> ( A = (/) \/ A = { B } ) ) $=
      ( vx csn wss c0 wceq wo wn wa wcel cv wex neq0 wb ssel elsni syl6 eleq1
      ibd 19.23adv syl5bi snssi anc2li eqss syl6ibr orrd 0ss sseq1 mpbiri
      eqimss jaoi impbii ) ABDZEZAFGZAUNGZHUOUPUQUOUPIZUOUNAEZJUQUOURUSUOURBAKZ
      USURCLZAKZCMUOUTCANUOVBUTCUOVBUTUOVBVABGZVBUTOUOVBVAUNKVCAUNVAPVABQRVABAS
      RTUAUBBAUCRUDAUNUEUFUGUPUOUQUPUOFUNEUNUHAFUNUIUJAUNUKULUM $.
      $( [24-Apr-2004] $)

    $( Two ways to express that a nonempty set equals a singleton. $)
    eqsn $p |- ( A =/= (/) -> ( A = { B } <-> A. x e. A x = B ) ) $=
      ( c0 wne csn wceq wss cv wral eqimss wn df-ne wo sssn biimpi ord syl5bi
      com12 impbid2 wcel dfss3 elsn ralbii bitri syl6bb ) BDEZBCFZGZBUHHZAIZCGZ
      ABJZUGUIUJBUHKUJUGUIUGBDGZLUJUIBDMUJUNUIUJUNUINBCOPQRSTUJUKUHUAZABJUMABUH
      UBUOULABACUCUDUEUF $.
      $( [15-Dec-2007] $)
  $}

  ${
    $( The subsets of a pair. $)
    sspr $p |- ( A C_ { B , C } <->
     ( ( A = (/) \/ A = { B } ) \/ ( A = { C } \/ A = { B , C } ) ) ) $=
      ( cpr wss c0 wceq csn wo wn wa simpll wcel pm2.45 anim1i ioran sylibr
      cdif difsn adantl ssdif difprsn a1i sstrd adantr eqsstr3d ex sssn syl6ib
      con1d imp sylan2 anassrs prcom sseq2i sylbi prssg ibi syl11anc eqssd
      orrd 0ss sseq1 mpbiri snsspr1 jaoi snsspr2 eqimss impbii ) ABCDZEZAFGZABH
      ZGZIZACHZGZAVJGZIZIVKVOVSVKVOJZVSVKVTKZVQVRWAVQJZVRWAWBKZAVJVKVTWBLWCBAMZ
      CAMZVJAEZVKVTWBWDVTWBKZVKVLVQIZJZWDWGVLJZWBKWIVTWJWBVLVNNOVLVQPQVKWIWDVKW
      DWHVKWDJZAVPEZWHVKWKWLVKWKKAAVMRZVPWKWMAGVKBASTVKWMVPEWKVKWMVJVMRZVPAVJVM
      UAWNVPEVKBCUBUCUDUEUFUGACUHUIUJUKULUMWAWEWBVKVTWEVKWEVOVKWEJZAVMEZVOVKWOW
      PVKWOKZAAVPRZVMWOWRAGVKCASTWQWRCBDZVPRZVMVKWRWTEZWOVKAWSEXAVJWSABCUNUOAWS
      VPUAUPUEWTVMEWQCBUBUCUDUFUGABUHUIUJUKUEWDWEKWFBCAAAUQURUSUTUGVAUGVAVOVKVS
      VLVKVNVLVKFVJEVJVBAFVJVCVDVNVKVMVJEBCVEAVMVJVCVDVFVQVKVRVQVKVPVJEBCVGAVPV
      JVCVDAVJVHVFVFVI $.
      $( [16-Mar-2006] $)
  $}

  ${
    $d A x $.  $d B x $.  $d C x $.  $d D x $.
    tpss.1 $e |- A e. _V $.
    tpss.2 $e |- B e. _V $.
    tpss.3 $e |- C e. _V $.
    $( A triplet of elements of a class is a subset of the class.  (The proof
       was shortened by Andrew Salmon, 29-Jun-2011.) $)
    tpss $p |- ( ( A e. D /\ B e. D /\ C e. D ) <-> { A , B , C } C_ D ) $=
      ( cpr wss csn wa cun wcel w3a ctp unss df-3an prss snss anbi12i bitri
      df-tp sseq1i 3bitr4i ) ABHZDIZCJZDIZKZUEUGLZDIADMZBDMZCDMZNZABCOZDIUEUGDP
      UNUKULKZUMKUIUKULUMQUPUFUMUHABDEFRCDGSTUAUOUJDABCUBUCUD $.
      $( [29-Jun-2011] $) $( [9-Apr-1994] $)
  $}

  ${
    sneqr.1 $e |- A e. _V $.
    $( If the singletons of two sets are equal, the two sets are equal.  Part
       of Exercise 4 of [TakeutiZaring] p. 15. $)
    sneqr $p |- ( { A } = { B } -> A = B ) $=
      ( csn wceq wcel snid eleq2 mpbii elsnc sylib ) ADZBDZEZAMFZABENALFOACGLMA
      HIABCJK $.
      $( [27-Aug-1993] $)

    $( If a singleton is a subset of another, their members are equal. $)
    snsssn $p |- ( { A } C_ { B } -> A = B ) $=
      ( csn wss c0 wceq wo sssn wne wn snnz df-ne mpbi pm2.21i sneqr jaoi
      sylbi ) ADZBDZESFGZSTGZHABGZSBIUAUCUBUAUCSFJUAKACLSFMNOABCPQR $.
      $( [28-May-2006] $)
  $}

  ${
    $d x A $.
    $( The singleton of a class is a subset of its power class. $)
    snsspw $p |- { A } C_ ~P A $=
      ( vx csn cpw cv wceq wss wcel eqimss elsn df-pw abeq2i 3imtr4i ssriv ) BA
      CZADZBEZAFQAGZQOHQPHQAIBAJRBPBAKLMN $.
      $( [5-Aug-1993] $)
  $}


  ${
    $d x A $.  $d x B $.  $d x C $.
    prsspw.1 $e |- A e. _V $.
    prsspw.2 $e |- B e. _V $.
    $( An unordered pair belongs to the power class of a class iff each member
       belongs to the class.  (The proof was shortened by Andrew Salmon,
       26-Jun-2011.) $)
    prsspw $p |- ( { A , B } C_ ~P C <-> ( A C_ C /\ B C_ C ) ) $=
      ( cpr cpw wss wcel wa prss elpw anbi12i bitr3i ) ABFCGZHAOIZBOIZJACHZBCHZ
      JABODEKPRQSACDLBCELMN $.
      $( [29-Jun-2011] $) $( [10-Dec-2003] $)
  $}

  ${
    preqr1.1 $e |- A e. _V $.
    preqr1.2 $e |- B e. _V $.
    $( Reverse equality lemma for unordered pairs.  If two unordered pairs have
       the same second element, the first elements are equal. $)
    preqr1 $p |- ( { A , C } = { B , C } -> A = B ) $=
      ( cpr wceq wcel wo prid1 eleq2 mpbii elpr sylib mpbiri eqcom eqeq2
      oplem1 ) ACFZBCFZGZABGZACGZBAGZBCGZUAATHZUBUCIUAASHUFACDJSTAKLABCDMNUABSH
      ZUDUEIUAUGBTHBCEJSTBKOBACEMNABPACBQR $.
      $( [18-Oct-1995] $)
  $}

  ${
    preqr2.1 $e |- A e. _V $.
    preqr2.2 $e |- B e. _V $.
    $( Reverse equality lemma for unordered pairs.  If two unordered pairs have
       the same first element, the second elements are equal. $)
    preqr2 $p |- ( { C , A } = { C , B } -> A = B ) $=
      ( cpr wceq prcom eqeq12i preqr1 sylbi ) CAFZCBFZGACFZBCFZGABGLNMOCAHCBHIA
      BCDEJK $.
      $( [5-Aug-1993] $)
  $}

  ${
    preq12b.1 $e |- A e. _V $.
    preq12b.2 $e |- B e. _V $.
    preq12b.3 $e |- C e. _V $.
    preq12b.4 $e |- D e. _V $.
    $( Equality relationship for two unordered pairs. $)
    preq12b $p |- ( { A , B } = { C , D } <->
                   ( ( A = C /\ B = D ) \/ ( A = D /\ B = C ) ) ) $=
      ( cpr wceq wa wo wcel prid1 eleq2 mpbii elpr sylib preq1 eqeq1d preqr2
      syl6bi com12 ancld wi prcom eqeq2i sylbi orim12d mpd preq12 syl6eq
      sylan9eq jaoi impbii ) ABIZCDIZJZACJZBDJZKZADJZBCJZKZLZURUSVBLZVEURAUQMZV
      FURAUPMVGABENUPUQAOPACDEQRURUSVAVBVDURUSUTUSURUTUSURCBIZUQJUTUSUPVHUQACBS
      TBDCFHUAUBUCUDURVBVCURUPDCIZJZVBVCUEUQVIUPCDUFUGVBVJVCVBVJDBIZVIJVCVBUPVK
      VIADBSZTBCDFGUAUBUCUHUDUIUJVAURVDABCDUKVBVCUPBDIZUQVBUPVKVMVLDBUFULBCDSUM
      UNUO $.
      $( [17-Oct-1996] $)

    $( Equality of two unordered pairs. $)
    prel12 $p |- ( -. A = B -> ( { A , B } = { C , D } <->
                   ( A e. { C , D } /\ B e. { C , D } ) ) ) $=
      ( wceq wn cpr wcel wa prid1 eleq2 mpbii prid2 jca wo wi elpr eqeq2
      notbid orel2 syl6bi com3l imp ancrd orel1 orim12d orcom bitri preq12b
      3imtr4g ex syl5bi imp3a impbid2 ) ABIZJZABKZCDKZIZAVBLZBVBLZMVCVDVEVCAVAL
      VDABENVAVBAOPVCBVALVEABFQVAVBBOPRUTVDVEVCVDACIZADIZSZUTVEVCTZACDEUAUTVHVI
      UTVHMZBDIZBCIZSZVFVKMZVGVLMZSVEVCVJVKVNVLVOVJVKVFUTVHVKVFTVKUTVHVFVKUTVGJ
      VHVFTVKUSVGBDAUBUCVGVFUDUEUFUGUHVJVLVGUTVHVLVGTVLUTVHVGVLUTVFJVHVGTVLUSVF
      BCAUBUCVFVGUIUEUFUGUHUJVEVLVKSVMBCDFUAVLVKUKULABCDEFGHUMUNUOUPUQUR $.
      $( [17-Oct-1996] $)

    $( A way to represent ordered pairs using unordered pairs with distinct
       members. $)
    opthpr $p |- ( A =/= D ->
                 ( { A , B } = { C , D } <-> ( A = C /\ B = D ) ) ) $=
      ( cpr wceq wa wo wne preq12b idd wn wi df-ne pm2.21 sylbi imp3a jaod orc
      impbid1 syl5bb ) ABICDIJACJBDJKZADJZBCJZKZLZADMZUFABCDEFGHNUKUJUFUKUFUFUI
      UKUFOUKUGUHUFUKUGPUGUHUFQZQADRUGULSTUAUBUFUIUCUDUE $.
      $( [27-Mar-2007] $)
  $}

  ${
    preqsn.1 $e |- A e. _V $.
    preqsn.2 $e |- B e. _V $.
    preqsn.3 $e |- C e. _V $.
    $( Equivalence for a pair equal to a singleton. $)
    preqsn $p |- ( { A , B } = { C } <-> ( A = B /\ B = C ) ) $=
      ( cpr csn wceq wa wo dfsn2 eqeq2i preq12b oridm eqtr3 simpr jca eqtr
      impbii bitri 3bitri ) ABGZCHZIUCCCGZIACIZBCIZJZUHKZABIZUGJZUDUEUCCLMABCCD
      EFFNUIUHUKUHOUHUKUHUJUGABCPUFUGQRUKUFUGABCSUJUGQRTUAUB $.
      $( [3-Jun-2008] $)
  $}

  $( Equality theorem for ordered pairs. $)
  opeq1 $p |- ( A = B -> <. A , C >. = <. B , C >. ) $=
    ( wceq csn cpr cop sneq preq1 preq12d df-op 3eqtr4g ) ABDZAEZACFZFBEZBCFZFA
    CGBCGMNPOQABHABCIJACKBCKL $.
    $( [25-Jun-1998] $)

  $( Equality theorem for ordered pairs. $)
  opeq2 $p |- ( A = B -> <. C , A >. = <. C , B >. ) $=
    ( wceq csn cpr cop preq2 preq2d df-op 3eqtr4g ) ABDZCEZCAFZFMCBFZFCAGCBGLNO
    MABCHICAJCBJK $.
    $( [25-Jun-1998] $)

  $( Equality theorem for ordered pairs. $)
  opeq12 $p |- ( ( A = C /\ B = D ) -> <. A , B >. = <. C , D >. ) $=
    ( wceq cop opeq1 opeq2 sylan9eq ) ACEBDEABFCBFCDFACBGBDCHI $.
    $( [28-May-1995] $)

  ${
    opeq1i.1 $e |- A = B $.
    $( Equality inference for ordered pairs. $)
    opeq1i $p |- <. A , C >. = <. B , C >. $=
      ( wceq cop opeq1 ax-mp ) ABEACFBCFEDABCGH $.
      $( [16-Dec-2006] $)

    $( Equality inference for ordered pairs. $)
    opeq2i $p |- <. C , A >. = <. C , B >. $=
      ( wceq cop opeq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [16-Dec-2006] $)

    ${
      opeq12i.2 $e |- C = D $.
      $( Equality inference for ordered pairs.  (The proof was shortened by
         Eric Schmidt, 4-Apr-2007.) $)
      opeq12i $p |- <. A , C >. = <. B , D >. $=
        ( wceq cop opeq12 mp2an ) ABGCDGACHBDHGEFACBDIJ $.
        $( [16-Dec-2006] $)
    $}
  $}

  ${
    opeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for ordered pairs. $)
    opeq1d $p |- ( ph -> <. A , C >. = <. B , C >. ) $=
      ( wceq cop opeq1 syl ) ABCFBDGCDGFEBCDHI $.
      $( [16-Dec-2006] $)

    $( Equality deduction for ordered pairs. $)
    opeq2d $p |- ( ph -> <. C , A >. = <. C , B >. ) $=
      ( wceq cop opeq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [16-Dec-2006] $)

    ${
      opeq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for ordered pairs.  (The proof was shortened by
         Andrew Salmon, 29-Jun-2011.) $)
      opeq12d $p |- ( ph -> <. A , C >. = <. B , D >. ) $=
        ( wceq cop opeq12 syl11anc ) ABCHDEHBDICEIHFGBDCEJK $.
        $( [29-Jun-2011] $) $( [16-Dec-2006] $)
    $}
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    hbop.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbop.2 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for ordered pairs. $)
    hbop $p |- ( y e. <. A , B >. -> A. x y e. <. A , B >. ) $=
      ( cop csn cpr df-op hbsn hbpr hbxfr ) ABCDGCHZCDIZICDJABNOABCEKABCDEFLLM
      $.
      $( [14-Nov-1995] $)
  $}

  ${
    $d y z B $.  $d y z A $.  $d y ph $.  $d x y z $.
    hbopd.1 $e |- ( ph -> A. x ph ) $.
    hbopd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbopd.3 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbop . $)
    hbopd $p |- ( ph -> ( y e. <. A , B >. -> A. x y e. <. A , B >. ) ) $=
      ( vz cv wcel wal cab cop wi hba1 hbab hbop a1i wceq 19.21aiv abidhb syl
      opeq1d opeq2d eqtrd eleq2d albid 3imtr3d ) ACJZIJZDKZBLZIMZUKEKZBLZIMZNZK
      ZUSBLZUJDENZKZVBBLUSUTOABCUNUQUMBICULBPQUPBICUOBPQRSAURVAUJAURDUQNVAAUNDU
      QAUJDKZVCBLOZCLUNDTAVDCGUABCIDUBUCUDAUQEDAUJEKZVEBLOZCLUQETAVFCHUABCIEUBU
      CUEUFUGZAUSVBBFVGUHUI $.
      $( [4-Feb-2008] $)
  $}

  ${
    $d B x $.  $d ps x $.
    ralunsn.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Restricted quantification over the union of a set and a singleton, using
       implicit substitution.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
    ralunsn $p |- ( B e. C -> ( A. x e. ( A u. { B } ) ph <->
                                ( A. x e. A ph /\ ps ) ) ) $=
      ( wcel csn cun wral wa wb ralunb a1i wsbc ralsng sbcieg bitrd anbi2d ) EF
      HZACDEIZJKZACDKZACUBKZLZUDBLUCUFMUAACDUBNOUAUEBUDUAUEACEPBACEFQABCEFGRSTS
      $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d C x $.  $d ch x $.  $d ps y $.  $d th x $.
    2ralunsn.1 $e |- ( x = B -> ( ph <-> ch ) ) $.
    2ralunsn.2 $e |- ( y = B -> ( ph <-> ps ) ) $.
    2ralunsn.3 $e |- ( x = B -> ( ps <-> th ) ) $.
    $( Double restricted quantification over the union of a set and a
       singleton, using implicit substitution.  (Contributed by Paul Chapman,
       17-Nov-2012.) $)
    2ralunsn $p |- ( B e. C ->
                     ( A. x e. ( A u. { B } ) A. y e. ( A u. { B } ) ph <->
                       ( ( A. x e. A A. y e. A ph /\ A. x e. A ps ) /\
                         ( A. y e. A ch /\ th ) ) ) ) $=
      ( wcel csn cun wral wa ralunsn ralbidv cv wceq anbi12d r19.26 anbi1i
      syl6bb bitrd ) HIMZAFGHNOZPZEUHPAFGPZBQZEUHPZUJEGPBEGPQZCFGPZDQZQZUGUIUKE
      UHABFGHIKRSUGULUKEGPZUOQUPUKUOEGHIETHUAZUJUNBDURACFGJSLUBRUQUMUOUJBEGUCUD
      UEUF $.
      $( [17-Nov-2012] $)
  $}

  $( Expansion of an ordered pair when the first member is a proper class.  See
     also ~ opprc1b , ~ opprc2 , ~ opprc3 . $)
  opprc1 $p |- ( -. A e. _V -> <. A , B >. = { (/) , { B } } ) $=
    ( cvv wcel wn cop csn cpr c0 df-op wceq snprc preq1 sylbi prprc1 preq2d
    eqtrd syl5eq ) ACDEZABFAGZABHZHZIBGZHZABJSUBIUAHZUDSTIKUBUEKALTIUAMNSUAUCIA
    BOPQR $.
    $( [10-Apr-2004] $)

  $( A property of an ordered pair of proper classes (due to our particular
     definition of ordered pair). $)
  opprc2 $p |- ( -. B e. _V -> <. A , B >. = <. A , A >. ) $=
    ( cvv wcel wn csn cpr cop prprc1 prcom dfsn2 3eqtr3g preq2d df-op 3eqtr4g
    ) BCDEZAFZABGZGQAAGZGABHAAHPRSQPBAGQRSBAIBAJAKLMABNAANO $.
    $( [15-Nov-1994] $)

  ${
    $d x A $.  $d x B $.
    $( The power set of a singleton. $)
    pwsn $p |- ~P { A } = { (/) , { A } } $=
      ( vx cv csn wss cab c0 wceq wo cpw cpr sssn abbii df-pw dfpr2 3eqtr4i )
      BCZADZEZBFQGHQRHIZBFRJGRKSTBQALMBRNBGROP $.
      $( [5-Jun-2006] $)

    $d x y $.  $d y A $.
    $( MAY BE REVISED - should we keep this? $)
    $( The power set of a singleton (direct proof). $)
    pwsnALT $p |- ~P { A } = { (/) , { A } } $=
      ( vx vy cv csn wss cab c0 wceq wo cpw cpr wn wa wcel wi wal dfss2 elsn
      imbi2i albii bitri wex neq0 exintr syl5bi df-clel exancom bitr2i snssi
      sylbi syl6 anc2li eqss syl6ibr orrd 0ss sseq1 mpbiri eqimss jaoi impbii
      abbii df-pw dfpr2 3eqtr4i ) BDZAEZFZBGVGHIZVGVHIZJZBGVHKHVHLVIVLBVIVLVIVJ
      VKVIVJMZVIVHVGFZNVKVIVMVNVICDZVGOZVOAIZPZCQZVMVNPVIVPVOVHOZPZCQVSCVGVHRWA
      VRCVTVQVPCASTUAUBVSVMVPVQNCUCZVNVMVPCUCVSWBCVGUDVPVQCUEUFWBAVGOZVNWCVQVPN
      CUCWBCAVGUGVQVPCUHUIAVGUJUKULUKUMVGVHUNUOUPVJVIVKVJVIHVHFVHUQVGHVHURUSVGV
      HUTVAVBVCBVHVDBHVHVEVF $.
      $( [8-Aug-2008] $) $( [5-Jun-2006] $)

    $( The power set of an unordered pair. $)
    pwpr $p |- ~P { A , B } = ( { (/) , { A } } u. { { B } , { A , B } } ) $=
      ( vx cpr cpw cv wss cab c0 csn cun wcel df-pw wo wceq visset elpr
      orbi12i elun sspr 3bitr4ri abbii abid2 3eqtri ) ABDZECFZUEGZCHUFIAJZDZBJZ
      UEDZKZLZCHULCUEMUGUMCUFUILZUFUKLZNUFIOUFUHONZUFUJOUFUEONZNUMUGUNUPUOUQUFI
      UHCPZQUFUJUEURQRUFUIUKSUFABTUAUBCULUCUD $.
      $( [1-May-2009] $)
  $}

  ${
    $d x y $.
    $( Compute the power set of the power set of the power set of the empty
       set.  (See also ~ pw0 and ~ pwpw0 .) $)
    pwpwpw0 $p |- ~P { (/) , { (/) } } =
                ( { (/) , { (/) } } u. { { { (/) } } , { (/) , { (/) } } } ) $=
      ( c0 csn pwpr ) AABC $.
      $( [29-Jul-2011] $) $( [2-May-2009] $)
  $}

  ${

    $( The power class of the universe is the universe.  Exercise 4.12(d) of
       [Mendelson] p. 235. $)
    pwv $p |- ~P _V = _V $=
      ( vx cvv cpw cv wcel wss ssv visset elpw mpbir 2th eqriv ) ABCZBADZMEZNBE
      ONBFNGNBAHZIJPKL $.
      $( [14-Sep-2003] $)
  $}

