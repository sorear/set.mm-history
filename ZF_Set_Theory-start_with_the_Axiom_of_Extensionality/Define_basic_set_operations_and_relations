$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Define basic set operations and relations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbols. $)
  $c \ $. $( Backslash (difference) $)
  $c u. $. $( Cup (union) $)
  $c i^i $. $( Cap (intersection) $)
  $c C_ $. $( Subclass or subset symbol $)
  $c C. $. $( Proper subclass or subset symbol $)

  $( Extend class notation to include class difference (read:  " ` A ` minus
     ` B ` "). $)
  cdif $a class ( A \ B ) $.

  $( Extend class notation to include union of two classes (read:  " ` A `
     union ` B ` "). $)
  cun $a class ( A u. B ) $.

  $( Extend class notation to include the intersection of two classes
     (read:  " ` A ` intersect ` B ` "). $)
  cin $a class ( A i^i B ) $.

  $( Extend wff notation to include the subclass relation.  This is
     read " ` A ` is a subclass of ` B ` " or " ` B ` includes ` A ` ."  When
     ` A ` exists as a set, it is also read " ` A ` is a subset of ` B ` ." $)
  wss $a wff A C_ B $.

  $( Extend wff notation with proper subclass relation. $)
  wpss $a wff A C. B $.

  ${
    $d x A $.  $d x B $.  $d y A $.  $d y B $.  $d z x $.  $d z y $.  $d z A $.
    $d z B $.
    $( Soundness justification theorem for ~ df-dif .  (Contributed by Rodolfo
       Medina, 27-Apr-2010.)  (The proof was shortened by Andrew Salmon,
       9-Jul-2011.) $)
    difjust $p |- { x | ( x e. A /\ -. x e. B ) }
                  = { y | ( y e. A /\ -. y e. B ) } $=
      ( vz cv wcel wn wa cab wceq eleq1 notbid anbi12d cbvabv eqtri ) AFZCGZQDG
      ZHZIZAJEFZCGZUBDGZHZIZEJBFZCGZUGDGZHZIZBJUAUFAEQUBKZRUCTUEQUBCLULSUDQUBDL
      MNOUFUKEBUBUGKZUCUHUEUJUBUGCLUMUDUIUBUGDLMNOP $.
      $( [9-Jul-2011] $) $( [27-Apr-2010] $)

    $( Soundness justification theorem for ~ df-dif .  (Contributed by Rodolfo
       Medina, 27-Apr-2010.) $)
    difjustOLD $p |- { x | ( x e. A /\ -. x e. B ) }
                  = { y | ( y e. A /\ -. y e. B ) } $=
      ( vz cv wcel wn wa cab wsbc df-clab sban clelsb3 sbn notbii bitri
      anbi12i 3bitri bitr4i eqriv ) EAFZCGZUBDGZHZIZAJZBFZCGZUHDGZHZIZBJZEFZUGG
      ZUNCGZUNDGZHZIZUNUMGZUOUFAUNKUCAUNKZUEAUNKZIUSUFEALUCUEAEMVAUPVBUREACNVBU
      DAUNKZHURUDAEOVCUQEADNPQRSUTULBUNKUIBUNKZUKBUNKZIUSULEBLUIUKBEMVDUPVEUREB
      CNVEUJBUNKZHURUJBEOVFUQEBDNPQRSTUA $.
      $( [27-Apr-2010] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Define class difference, also called relative complement.  Definition
       5.12 of [TakeutiZaring] p. 20.  Several notations are used in the
       literature; we chose the ` \ ` convention used in Definition 5.3 of
       [Eisenberg] p. 67 instead of the more common minus sign to reserve the
       latter for later use in, e.g., arithmetic.  We will use the
       terminology " ` A ` excludes ` B ` " to mean ` A \ B ` .  We will
       use " ` B ` is removed from ` A ` " to mean ` A \ { B } ` i.e. the
       removal of an element or equivalently the exclusion of a singleton. $)
    df-dif $a |- ( A \ B ) = { x | ( x e. A /\ -. x e. B ) } $.
  $}

  ${
    $d x A $.  $d x B $.  $d y A $.  $d y B $.  $d z x $.  $d z y $.  $d z A $.
    $d z B $.
    $( Soundness justification theorem for ~ df-un .  (Contributed by Rodolfo
       Medina, 28-Apr-2010.)  (The proof was shortened by Andrew Salmon,
       9-Jul-2011.) $)
    unjust $p |- { x | ( x e. A \/ x e. B ) } = { y | ( y e. A \/ y e. B ) } $=
      ( vz cv wcel wo cab wceq eleq1 orbi12d cbvabv eqtri ) AFZCGZODGZHZAIEFZCG
      ZSDGZHZEIBFZCGZUCDGZHZBIRUBAEOSJPTQUAOSCKOSDKLMUBUFEBSUCJTUDUAUESUCCKSUCD
      KLMN $.
      $( [9-Jul-2011] $) $( [28-Apr-2010] $)

    $( Soundness justification theorem for ~ df-un .  (Contributed by Rodolfo
       Medina, 28-Apr-2010.) $)
    unjustOLD $p |- { x | ( x e. A \/ x e. B ) } = { y | ( y e. A \/ y e. B )
      } $=
      ( vz cv wcel wo cab wsbc df-clab sbor clelsb3 orbi12i 3bitri bitr4i
      eqriv ) EAFZCGZRDGZHZAIZBFZCGZUCDGZHZBIZEFZUBGZUHCGZUHDGZHZUHUGGZUIUAAUHJ
      SAUHJZTAUHJZHULUAEAKSTAELUNUJUOUKEACMEADMNOUMUFBUHJUDBUHJZUEBUHJZHULUFEBK
      UDUEBELUPUJUQUKEBCMEBDMNOPQ $.
      $( [28-Apr-2010] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Define the union of two classes.  Definition 5.6 of [TakeutiZaring]
       p. 16.  For an alternate definition in terms of class difference,
       requiring no dummy variables, see ~ dfun2 .  For union defined in terms
       of intersection, see ~ dfun3 . $)
    df-un $a |- ( A u. B ) = { x | ( x e. A \/ x e. B ) } $.
  $}

  ${
    $d x A $.  $d x B $.  $d y A $.  $d y B $.  $d z x $.  $d z y $.  $d z A $.
    $d z B $.
    $( Soundness justification theorem for ~ df-in .  (Contributed by Rodolfo
       Medina, 28-Apr-2010.)  (The proof was shortened by Andrew Salmon,
       9-Jul-2011.) $)
    injust $p |- { x | ( x e. A /\ x e. B ) }
                  = { y | ( y e. A /\ y e. B ) } $=
      ( vz cv wcel wa cab wceq eleq1 anbi12d cbvabv eqtri ) AFZCGZODGZHZAIEFZCG
      ZSDGZHZEIBFZCGZUCDGZHZBIRUBAEOSJPTQUAOSCKOSDKLMUBUFEBSUCJTUDUAUESUCCKSUCD
      KLMN $.
      $( [9-Jul-2011] $) $( [28-Apr-2010] $)

    $( Soundness justification theorem for ~ df-in .  (Contributed by Rodolfo
       Medina, 28-Apr-2010.) $)
    injustOLD $p |- { x | ( x e. A /\ x e. B ) }
                  = { y | ( y e. A /\ y e. B ) } $=
      ( vz cv wcel wa cab wsbc df-clab sban clelsb3 anbi12i 3bitri bitr4i
      eqriv ) EAFZCGZRDGZHZAIZBFZCGZUCDGZHZBIZEFZUBGZUHCGZUHDGZHZUHUGGZUIUAAUHJ
      SAUHJZTAUHJZHULUAEAKSTAELUNUJUOUKEACMEADMNOUMUFBUHJUDBUHJZUEBUHJZHULUFEBK
      UDUEBELUPUJUQUKEBCMEBDMNOPQ $.
      $( [28-Apr-2010] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Define the intersection of two classes.  Definition 5.6 of
       [TakeutiZaring] p. 16.  For alternate definitions in terms of class
       difference, requiring no dummy variables, see ~ dfin2 and ~ dfin4 .  For
       intersection defined in terms of union, see ~ dfin3 . $)
    df-in $a |- ( A i^i B ) = { x | ( x e. A /\ x e. B ) } $.

    $( Alternate definition for the intersection of two classes. $)
    dfin5 $p |- ( A i^i B ) = { x e. A | x e. B } $=
      ( cin cv wcel wa cab crab df-in df-rab eqtr4i ) BCDAEZBFMCFZGAHNABIABCJNA
      BKL $.
      $( [6-Jul-2005] $)
  $}

  $( Define the subclass relationship.  Exercise 9 of [TakeutiZaring] p. 18.
     For a more traditional definition, but requiring a dummy variable, see
     ~ dfss2 .  Other possible definitions are given by ~ dfss3 , ~ dfss4 ,
     ~ sspss , ~ ssequn1 , ~ ssequn2 , ~ sseqin2 , and ~ ssdif0 . $)
  df-ss $a |- ( A C_ B <-> ( A i^i B ) = A ) $.

  $( A frequently-used variant of subclass definition ~ df-ss . $)
  dfss $p |- ( A C_ B <-> A = ( A i^i B ) ) $=
    ( wss cin wceq df-ss eqcom bitri ) ABCABDZAEAIEABFIAGH $.
    $( [3-Sep-2004] $)

  $( Define proper subclass relationship between two classes.  Definition 5.9
     of [TakeutiZaring] p. 17.  Other possible definitions are given by
     ~ dfpss2 and ~ dfpss3 . $)
  df-pss $a |- ( A C. B <-> ( A C_ B /\ A =/= B ) ) $.

  ${
    $d x A $.  $d x B $.
    $( Alternate definition of class difference. $)
    dfdif2 $p |- ( A \ B ) = { x e. A | -. x e. B } $=
      ( cdif cv wcel wn wa cab crab df-dif df-rab eqtr4i ) BCDAEZBFNCFGZHAIOABJ
      ABCKOABLM $.
      $( [25-Mar-2004] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Expansion of membership in a class difference. $)
    eldif $p |- ( A e. ( B \ C ) <-> ( A e. B /\ -. A e. C ) ) $=
      ( vx cdif wcel cvv wn wa elisset adantr cv wceq eleq1 notbid anbi12d
      df-dif elab2g pm5.21nii ) ABCEZFAGFZABFZACFZHZIZATJUBUAUDABJKDLZBFZUFCFZH
      ZIUEDATGUFAMZUGUBUIUDUFABNUJUHUCUFACNOPDBCQRS $.
      $( [29-Apr-1994] $)
  $}

