$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CVec $.

  $( Extend class notation with the class of all complex vector spaces. $)
  cvc $a class CVec $.

  ${
    $d g s x y z $.
    $( Define the class of all complex vector spaces. $)
    df-vc $a |- CVec = { <. g , s >. | ( g e. Abel /\
     s :  ( CC X. ran g ) --> ran g /\ A. x e. ran g ( ( 1 s x ) = x /\
       A. y e. CC ( A. z e. ran g ( y s ( x g z ) ) = ( ( y s x ) g ( y s z ) )
         /\ A. z e. CC ( ( ( y + z ) s x ) = ( ( y s x ) g ( z s x ) ) /\
           ( ( y x. z ) s x ) = ( y s ( z s x ) ) ) ) ) ) } $.

    $( The class of all complex vector spaces is a relation. $)
    vcrel $p |- Rel CVec $=
      ( vg vs vx vy vz cvc wrel cv cabl wcel cc crn cxp wf c1 co wceq wral
      caddc cmul wa w3a copab relopab wb df-vc releq ax-mp mpbir ) FGZAHZIJKUKL
      ZMULBHZNOCHZUMPUNQDHZUNEHZUKPUMPUOUNUMPZUOUPUMPUKPQEULRUOUPSPUNUMPUQUPUNU
      MPZUKPQUOUPTPUNUMPUOURUMPQUAEKRUADKRUACULRUBZABUCZGZUSABUDFUTQUJVAUECDEAB
      UFFUTUGUHUI $.
      $( [6-Apr-2007] $) $( [17-Mar-2007] $)
  $}

  ${
    $d g s x y z G $.  $d g s x y z S $.  $d g s x z X $.
    isvcg.1 $e |- X = ran G $.
    $( The predicate "is a complex vector space." $)
    isvcg $p |- ( S e. A -> ( <. G , S >. e. CVec <-> ( G e. Abel /\
         S :  ( CC X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. CC ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. CC ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) ) $=
      ( vg vs wcel cop cvc cabl cc cxp wf c1 cv co wceq wral caddc cmul wa w3a
      cvv wbr df-br wrel crn copab relopab df-vc releqi mpbir brrelexi sylbir
      anim1i ancoms elisset adantrr 3adantr3 eleq1 wb rneq syl6eqr xpeq2 feq2
      syl feq3 bitrd raleq1d opreq opreq2d eqeq12d ralbidv eqeq2d anbi1d
      anbi12d anbi2d 3anbi123d feq1 eqeq1d opreq12d eqtrd 3anbi23d opelopabg
      eleq2i syl5bb pm5.21nd ) EDKZFELZMKZFNKZOGPZGEQZRASZETZWRUAZBSZWRCSZFTZET
      ZXAWRETZXAXBETZFTZUAZCGUBZXAXBUCTZWRETZXEXBWRETZFTZUAZXAXBUDTZWRETZXAXLET
      ZUAZUEZCOUBZUEZBOUBZUEZAGUBZUFZFUGKZWLUEZWNWLYGWNYFWLWNFEMUHYFFEMUIFEMMUJ
      ISZNKZOYHUKZPZYJJSZQZRWRYLTZWRUAZXAWRXBYHTZYLTZXAWRYLTZXAXBYLTZYHTZUAZCYJ
      UBZXJWRYLTZYRXBWRYLTZYHTZUAZXOWRYLTZXAUUDYLTZUAZUEZCOUBZUEZBOUBZUEZAYJUBZ
      UFZIJULZUJUUPIJUMMUUQABCIJUNZUOUPUQURUSUTWLWOWQYGYDWLWOYGWQWOWLYGWOYFWLFN
      VAUSUTVBVCYGWMUUQKYEWNUUPWOWPGYLQZYOXAXCYLTZYRYSFTZUAZCGUBZUUCYRUUDFTZUAZ
      UUIUEZCOUBZUEZBOUBZUEZAGUBZUFYEIJFEUGDYHFUAZYIWOYMUUSUUOUVKYHFNVDUVLYJGUA
      ZYMUUSVEUVLYJFUKGYHFVFHVGZUVMYMWPYJYLQZUUSUVMYKWPUAYMUVOVEYJGOVHYKWPYJYLV
      IVJYJGWPYLVKVLVJUVLUUOUUNAGUBUVKUVLUUNAYJGUVNVMUVLUUNUVJAGUVLUUMUVIYOUVLU
      ULUVHBOUVLUUBUVCUUKUVGUVLUUBUUACGUBUVCUVLUUACYJGUVNVMUVLUUAUVBCGUVLYQUUTY
      TUVAUVLYPXCXAYLWRXBYHFVNVOYRYSYHFVNVPVQVLUVLUUJUVFCOUVLUUFUVEUUIUVLUUEUVD
      UUCYRUUDYHFVNVRVSVQVTVQWAVQVLWBYLEUAZUUSWQUVKYDWOWPGYLEWCUVPUVJYCAGUVPYOW
      TUVIYBUVPYNWSWRRWRYLEVNWDUVPUVHYABOUVPUVCXIUVGXTUVPUVBXHCGUVPUUTXDUVAXGXA
      XCYLEVNUVPYRXEYSXFFXAWRYLEVNZXAXBYLEVNWEVPVQUVPUVFXSCOUVPUVEXNUUIXRUVPUUC
      XKUVDXMXJWRYLEVNUVPYRXEUUDXLFUVQXBWRYLEVNZWEVPUVPUUGXPUUHXQXOWRYLEVNUVPUU
      HXAUUDETXQXAUUDYLEVNUVPUUDXLXAEUVRVOWFVPVTVQVTVQVTVQWGWHMUUQWMUURWIWJWK
      $.
      $( [9-Nov-2006] $) $( [3-Nov-2006] $)
  $}

  ${
    $d x y z G $.  $d x y z S $.  $d x y z X $.
    isvci.1 $e |- G e. Abel $.
    isvci.2 $e |- G : ( X X. X ) --> X $.
    isvci.3 $e |- S : ( CC X. X ) --> X $.
    isvci.4 $e |- ( x e. X -> ( 1 S x ) = x ) $.
    isvci.5 $e |- ( ( y e. CC /\ x e. X /\ z e. X ) ->
                  ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) ) ) $.
    isvci.6 $e |- ( ( y e. CC /\ z e. CC /\ x e. X ) ->
                  ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) ) $.
    isvci.7 $e |- ( ( y e. CC /\ z e. CC /\ x e. X ) ->
                  ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) $.
    isvci.8 $e |- W = <. G , S >. $.
    $( Properties that determine a complex vector space. $)
    isvci $p |- W e. CVec $=
      ( cop cvc wcel cabl cc cxp wf c1 cv co wceq wral caddc cmul wa w3a
      3com12 3expa r19.21aiva jca 3comr rgen 3pm3.2i cvv wb axcnex crn cgr
      ablgrp ax-mp grprn rnexg eqeltr xpex fex mp2an isvcg mpbir ) FEDPZQOVNQRZ
      ESRZTGUAZGDUBZUCAUDZDUEVSUFZBUDZVSCUDZEUEDUEWAVSDUEZWAWBDUEEUEUFZCGUGZWAW
      BUHUEVSDUEWCWBVSDUEZEUEUFZWAWBUIUEVSDUEWAWFDUEUFZUJZCTUGZUJZBTUGZUJZAGUGZ
      UKZVPVRWNHJWMAGVSGRZVTWLKWPWKBTWPWATRZUJZWEWJWRWDCGWPWQWBGRZWDWQWPWSWDLUL
      UMUNWRWICTWPWQWBTRZWIWQWTWPWIWQWTWPUKWGWHMNUOUPUMUNUOUNUOUQURDUSRZVOWOUTV
      RVQUSRXAJTGVAGEVBZUSEGVPEVCRHEVDVEIVFZVPXBUSRHESVGVEVHVIVQGUSDVJVKABCUSDE
      GXCVLVEVMVH $.
      $( [9-Nov-2006] $) $( [5-Nov-2006] $)
  $}

  ${
    $d g s w x y z G $.  $d g s w x y z S $.  $d g s W $.  $d g s w x y z X $.
    $d w x y z A $.  $d w x y z B $.  $d x y z C $.
    vci.1 $e |- G = ( 1st ` W ) $.
    vci.2 $e |- S = ( 2nd ` W ) $.
    vci.3 $e |- X = ran G $.
    $( The properties of a complex vector space, which is an Abelian group
       (i.e. the vectors, with the operation of vector addition) accompanied
       by a scalar multiplication operation on the field of complex numbers.
       The variable ` W ` was chosen because ` V ` is already used for the
       universal class. $)
    vci $p |- ( W e. CVec ->
              ( G e. Abel /\
         S :  ( CC X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. CC ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. CC ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) $=
      ( vg vs cvc wcel cv cabl cc crn cxp wf c1 co wceq wral caddc cmul wa w3a
      copab df-vc eleq2i c1st cfv wb eqeq2i eleq1 rneq syl6eqr xpeq2 feq2 syl
      feq3 bitrd raleq1d opreq opreq2d eqeq12d ralbidv eqeq2d anbi1d anbi12d
      anbi2d 3anbi123d sylbir c2nd feq1 eqeq1d opreq12d eqtrd 3anbi23d elopabi
      sylbi ) FMNFKOZPNZQWCRZSZWELOZTZUAAOZWGUBZWIUCZBOZWICOZWCUBZWGUBZWLWIWGUB
      ZWLWMWGUBZWCUBZUCZCWEUDZWLWMUEUBZWIWGUBZWPWMWIWGUBZWCUBZUCZWLWMUFUBZWIWGU
      BZWLXCWGUBZUCZUGZCQUDZUGZBQUDZUGZAWEUDZUHZKLUIZNEPNZQGSZGDTZUAWIDUBZWIUCZ
      WLWIWMEUBZDUBZWLWIDUBZWLWMDUBZEUBZUCZCGUDZXAWIDUBZYEWMWIDUBZEUBZUCZXFWIDU
      BZWLYKDUBZUCZUGZCQUDZUGZBQUDZUGZAGUDZUHZMXQFABCKLUJUKXPXRXSGWGTZWKWLYCWGU
      BZWPWQEUBZUCZCGUDZXBWPXCEUBZUCZXIUGZCQUDZUGZBQUDZUGZAGUDZUHZUUCKLFWCFULUM
      ZUCWCEUCZXPUUQUNEUURWCHUOUUSWDXRWHUUDXOUUPWCEPUPUUSWEGUCZWHUUDUNUUSWEERGW
      CEUQJURZUUTWHXSWEWGTZUUDUUTWFXSUCWHUVBUNWEGQUSWFXSWEWGUTVAWEGXSWGVBVCVAUU
      SXOXNAGUDUUPUUSXNAWEGUVAVDUUSXNUUOAGUUSXMUUNWKUUSXLUUMBQUUSWTUUHXKUULUUSW
      TWSCGUDUUHUUSWSCWEGUVAVDUUSWSUUGCGUUSWOUUEWRUUFUUSWNYCWLWGWIWMWCEVEVFWPWQ
      WCEVEVGVHVCUUSXJUUKCQUUSXEUUJXIUUSXDUUIXBWPXCWCEVEVIVJVHVKVHVLVHVCVMVNWGF
      VOUMZUCWGDUCZUUQUUCUNDUVCWGIUOUVDUUDXTUUPUUBXRXSGWGDVPUVDUUOUUAAGUVDWKYBU
      UNYTUVDWJYAWIUAWIWGDVEVQUVDUUMYSBQUVDUUHYIUULYRUVDUUGYHCGUVDUUEYDUUFYGWLY
      CWGDVEUVDWPYEWQYFEWLWIWGDVEZWLWMWGDVEVRVGVHUVDUUKYQCQUVDUUJYMXIYPUVDXBYJU
      UIYLXAWIWGDVEUVDWPYEXCYKEUVEWMWIWGDVEZVRVGUVDXGYNXHYOXFWIWGDVEUVDXHWLYKWG
      UBYOUVDXCYKWLWGUVFVFWLYKWGDVEVSVGVKVHVKVHVKVHVTVNWAWB $.
      $( [4-Nov-2006] $) $( [3-Nov-2006] $)

    $( Functionality of th scalar product of a complex vector space. $)
    vcsm $p |- ( W e. CVec -> S : ( CC X. X ) --> X ) $=
      ( vx vy vz cvc wcel cabl cc cxp wf c1 cv co wceq wral caddc cmul wa w3a
      vci 3simp2 syl ) CKLBMLZNDODAPZQHRZASUKTIRZUKJRZBSASULUKASZULUMASBSTJDUAU
      LUMUBSUKASUNUMUKASZBSTULUMUCSUKASULUOASTUDJNUAUDINUAUDHDUAZUEUJHIJABCDEFG
      UFUIUJUPUGUH $.
      $( [5-Nov-2006] $) $( [3-Nov-2006] $)

    $( Closure of the scalar product of a complex vector space. $)
    vccl $p |- ( ( W e. CVec /\ A e. CC /\ B e. X ) -> ( A S B ) e. X ) $=
      ( cc cxp wf wcel co cvc foprrn vcsm syl3an1 ) JFKFCLAJMBFMABCNFMEOMABFJFC
      PCDEFGHIQR $.
      $( [11-Nov-2006] $) $( [3-Nov-2006] $)

    $( Identity element for the scalar product of a complex vector space. $)
    vcid $p |- ( ( W e. CVec /\ A e. X ) -> ( 1 S A ) = A ) $=
      ( vx vy vz c1 cv co wceq wral wcel cvc opreq2 id eqeq12d rcla4cva cabl
      cc cxp wf caddc cmul wa w3a vci pm3.26 r19.20si 3ad2ant3 syl sylan ) LIMZ
      BNZUQOZIEPZAEQLABNZAOZDRQZUSVBIAEUQAOZURVAUQAUQALBSVDTUAUBVCCUCQZUDEUEEBU
      FZUSJMZUQKMZCNBNVGUQBNZVGVHBNCNOKEPVGVHUGNUQBNVIVHUQBNZCNOVGVHUHNUQBNVGVJ
      BNOUIKUDPUIJUDPZUIZIEPZUJUTIJKBCDEFGHUKVMVEUTVFVLUSIEUSVKULUMUNUOUP $.
      $( [4-Nov-2006] $) $( [3-Nov-2006] $)

    $( Distributive law for the scalar product of a complex vector space. $)
    vcdi $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
       ( A S ( B G C ) ) = ( ( A S B ) G ( A S C ) ) ) $=
      ( vy vx vz cc wcel w3a cvc co wceq wi cv wral opreq1 opreq2d opreq2
      opreq1d eqeq12d opreq12d rcla43v cabl cxp wf c1 caddc cmul wa vci pm3.26
      r19.20si adantl 3ad2ant3 syl syl5 3com12 impcom ) ANOZBGOZCGOZPFQOZABCERZ
      DRZABDRZACDRZERZSZVGVFVHVIVOTVGVFVHPKUAZLUAZMUAZERZDRZVPVQDRZVPVRDRZERZSZ
      MGUBZKNUBZLGUBZVOVIWDVOVPBVRERZDRZVPBDRZWBERZSAWHDRZVLAVRDRZERZSLKMBACGNG
      VQBSZVTWIWCWKWOVSWHVPDVQBVREUCUDWOWAWJWBEVQBVPDUEUFUGVPASZWIWLWKWNVPAWHDU
      CWPWJVLWBWMEVPABDUCVPAVRDUCUHUGVRCSZWLVKWNVNWQWHVJADVRCBEUEUDWQWMVMVLEVRC
      ADUEUDUGUIVIEUJOZNGUKGDULZUMVQDRVQSZWEVPVRUNRVQDRWAVRVQDRZERSVPVRUORVQDRV
      PXADRSUPMNUBZUPZKNUBZUPZLGUBZPWGLKMDEFGHIJUQXFWRWGWSXEWFLGXDWFWTXCWEKNWEX
      BURUSUTUSVAVBVCVDVE $.
      $( [25-Nov-2006] $) $( [3-Nov-2006] $)

    $( Distributive law for the scalar product of a complex vector space. $)
    vcdir $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A + B ) S C ) = ( ( A S C ) G ( B S C ) ) ) $=
      ( vy vz vx cc wcel w3a cvc caddc co wceq wi cv wral opreq2 opreq12d
      eqeq12d opreq1 opreq1d opreq2d rcla43v cabl cxp wf c1 cmul wa vci pm3.26
      r19.20si adantl 3ad2ant3 syl syl5 3coml impcom ) ANOZBNOZCGOZPFQOZABRSZCD
      SZACDSZBCDSZESZTZVHVFVGVIVOUAVHVFVGPKUBZLUBZRSZMUBZDSZVPVSDSZVQVSDSZESZTZ
      LNUCZKNUCZMGUCZVOVIWDVOVRCDSZVPCDSZVQCDSZESZTAVQRSZCDSZVLWJESZTMKLCABGNNV
      SCTZVTWHWCWKVSCVRDUDWOWAWIWBWJEVSCVPDUDVSCVQDUDUEUFVPATZWHWMWKWNWPVRWLCDV
      PAVQRUGUHWPWIVLWJEVPACDUGUHUFVQBTZWMVKWNVNWQWLVJCDVQBARUDUHWQWJVMVLEVQBCD
      UGUIUFUJVIEUKOZNGULGDUMZUNVSDSVSTZVPVSVQESDSWAVPVQDSESTLGUCZWDVPVQUOSVSDS
      VPWBDSTZUPZLNUCZUPZKNUCZUPZMGUCZPWGMKLDEFGHIJUQXHWRWGWSXGWFMGXFWFWTXEWEKN
      XDWEXAXCWDLNWDXBURUSUTUSUTUSVAVBVCVDVE $.
      $( [12-Nov-2006] $) $( [3-Nov-2006] $)

    $( Associative law for the scalar product of a complex vector space. $)
    vcass $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A x. B ) S C ) = ( A S ( B S C ) ) ) $=
      ( vy vz vx cc wcel w3a cvc cmul co wceq wi cv wral opreq2 opreq2d
      eqeq12d opreq1 opreq1d rcla43v cabl cxp wf c1 caddc wa vci pm3.27
      r19.20si adantl 3ad2ant3 syl syl5 3coml impcom ) ANOZBNOZCGOZPFQOZABRSZCD
      SZABCDSZDSZTZVGVEVFVHVMUAVGVEVFPKUBZLUBZRSZMUBZDSZVNVOVQDSZDSZTZLNUCZKNUC
      ZMGUCZVMVHWAVMVPCDSZVNVOCDSZDSZTAVORSZCDSZAWFDSZTMKLCABGNNVQCTZVRWEVTWGVQ
      CVPDUDWKVSWFVNDVQCVODUDUEUFVNATZWEWIWGWJWLVPWHCDVNAVORUGUHVNAWFDUGUFVOBTZ
      WIVJWJVLWMWHVICDVOBARUDUHWMWFVKADVOBCDUGUEUFUIVHEUJOZNGUKGDULZUMVQDSVQTZV
      NVQVOESDSVNVQDSZVNVODSESTLGUCZVNVOUNSVQDSWQVSESTZWAUOZLNUCZUOZKNUCZUOZMGU
      CZPWDMKLDEFGHIJUPXEWNWDWOXDWCMGXCWCWPXBWBKNXAWBWRWTWALNWSWAUQURUSURUSURUT
      VAVBVCVD $.
      $( [25-Nov-2006] $) $( [3-Nov-2006] $)

    $( A vector plus itself is two times the vector. $)
    vc2 $p |- ( ( W e. CVec /\ A e. X ) -> ( A G A ) = ( 2 S A ) ) $=
      ( cvc wcel wa c1 co c2 vcid opreq12d caddc cc wceq ax1cn vcdir mp3anr1
      mpanr1 df-2 opreq1i syl5req eqtr3d ) DIJZAEJZKZLABMZUKCMZAACMNABMZUJUKAUK
      ACABCDEFGHOZUNPUJLLQMZABMZULUMUHLRJZUIUPULSZTUHUQUQUIURTLLABCDEFGHUAUBUCN
      UOABUDUEUFUG $.
      $( [1-Feb-2007] $) $( [1-Feb-2007] $)
  $}

  ${
    $d x y z G $.  $d x y z W $.
    vcabl.1 $e |- G = ( 1st ` W ) $.
    $( Vector addition is an Abelian group operation. $)
    vcabl $p |- ( W e. CVec -> G e. Abel ) $=
      ( vx vy vz cvc wcel cabl cc crn cxp c2nd cfv wf c1 cv co wceq wral caddc
      cmul wa eqid vci 3simp1d ) BGHAIHJAKZLUGBMNZOPDQZUHRUISEQZUIFQZARUHRUJUIU
      HRZUJUKUHRARSFUGTUJUKUARUIUHRULUKUIUHRZARSUJUKUBRUIUHRUJUMUHRSUCFJTUCEJTU
      CDUGTDEFUHABUGCUHUDUGUDUEUF $.
      $( [12-Nov-2006] $) $( [3-Nov-2006] $)

    $( Vector addition is a group operation. $)
    vcgrp $p |- ( W e. CVec -> G e. Grp ) $=
      ( cvc wcel cabl cgr vcabl ablgrp syl ) BDEAFEAGEABCHAIJ $.
      $( [14-Nov-2006] $) $( [4-Nov-2006] $)
  $}

  ${
    vcgcl.1 $e |- G = ( 1st ` W ) $.
    vcgcl.2 $e |- X = ran G $.
    $( Closure law for the vector addition (group) operation of a complex
       vector space. $)
    vcgcl $p |- ( ( W e. CVec /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cgr wcel co cvc grpcl vcgrp syl3an1 ) CHIAEIBEIABCJEIDKIABCEGLCDFMN $.
      $( [5-Feb-2007] $) $( [1-Feb-2007] $)
  $}

  ${
    vccom.1 $e |- G = ( 1st ` W ) $.
    vccom.2 $e |- X = ran G $.
    $( Vector addition is commutative. $)
    vccom $p |- ( ( W e. CVec /\ A e. X /\ B e. X ) ->
                ( A G B ) = ( B G A ) ) $=
      ( cabl wcel co wceq cvc ablcom vcabl syl3an1 ) CHIAEIBEIABCJBACJKDLIABCEG
      MCDFNO $.
      $( [29-Nov-2006] $) $( [4-Nov-2006] $)

    $( Vector addition is associative. $)
    vcaass $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cgr wcel w3a co wceq cvc grpass vcgrp sylan ) DIJAFJBFJCFJKABDLCDLABCDL
      DLMENJABCDFHODEGPQ $.
      $( [8-Dec-2006] $) $( [4-Nov-2006] $)

    $( Right cancellation law for vector addition. $)
    vcrcan $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cgr wcel w3a co wceq wb cvc grprcan vcgrp sylan ) DIJAFJBFJCFJKACDLBCDL
      MABMNEOJABCDFHPDEGQR $.
      $( [11-Apr-2007] $) $( [4-Nov-2006] $)

    $( Left cancellation law for vector addition. $)
    vclcan $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cgr wcel w3a co wceq wb cvc grplcan vcgrp sylan ) DIJAFJBFJCFJKCADLCBDL
      MABMNEOJABCDFHPDEGQR $.
      $( [14-Nov-2006] $) $( [4-Nov-2006] $)
  $}

  ${
    vc0cl.1 $e |- G = ( 1st ` W ) $.
    vc0cl.2 $e |- X = ran G $.
    vc0cl.3 $e |- Z = ( Id ` G ) $.
    $( The zero vector is a vector. $)
    vc0cl $p |- ( W e. CVec -> Z e. X ) $=
      ( cvc wcel cgr vcgrp grpidcl syl ) BHIAJIDCIABEKDACFGLM $.
      $( [15-Nov-2006] $) $( [4-Nov-2006] $)

    $( The zero vector is a right identity element. $)
    vc0rid $p |- ( ( W e. CVec /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cgr wcel co wceq cvc grprid vcgrp sylan ) BIJADJAEBKALCMJAEBDGHNBCFOP
      $.
      $( [15-Nov-2006] $) $( [4-Nov-2006] $)

    $( The zero vector is a left identity element. $)
    vc0lid $p |- ( ( W e. CVec /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cgr wcel co wceq cvc grplid vcgrp sylan ) BIJADJEABKALCMJAEBDGHNBCFOP
      $.
      $( [?] $) $( [26-Apr-2007] $)
  $}

  ${
    vc0.1 $e |- G = ( 1st ` W ) $.
    vc0.2 $e |- S = ( 2nd ` W ) $.
    vc0.3 $e |- X = ran G $.
    vc0.4 $e |- Z = ( Id ` G ) $.
    $( Zero times a vector is the zero vector.  Equation 1a of [Kreyszig]
       p. 51. $)
    vc0 $p |- ( ( W e. CVec /\ A e. X ) -> ( 0 S A ) = Z ) $=
      ( cvc wcel wa cc0 co wceq c1 vc0rid caddc ax1cn addid1 opreq1i a1i cc
      0cn vcdir mp3anr1 mpanr1 vcid 3eqtr3d opreq1d 3eqtr2rd w3a wb vccl
      mp3an2 vc0cl adantr pm3.27 3jca vclcan syldan mpbid ) DKLZAELZMZANABOZCOZ
      AFCOZPZVGFPZVFVIAQABOZVGCOZVHACDEFGIJRVFQNSOZABOZVLVMAVOVLPVFVNQABQTUAUBU
      CVDNUDLZVEVOVMPZUEVDQUDLVPVEVQTQNABCDEGHIUFUGUHABCDEGHIUIZUJVFVLAVGCVRUKU
      LVDVEVGELZFELZVEUMVJVKUNVFVSVTVEVDVPVEVSUENABCDEGHIUOUPVDVTVECDEFGIJUQURV
      DVEUSUTVGFACDEGIVAVBVC $.
      $( [24-Nov-2006] $) $( [4-Nov-2006] $)

    $( Anything times the zero vector is the zero vector.  Equation 1b of
       [Kreyszig] p. 51. $)
    vcz $p |- ( ( W e. CVec /\ A e. CC ) -> ( A S Z ) = Z ) $=
      ( cvc wcel cc wa co cc0 cmul wceq vc0cl anim2i ancoms 0cn vcass mp3anr2
      syldan vc0 mpdan opreq2d adantr eqtr2d mul01t opreq1d adantl 3eqtrd ) DKL
      ZAMLZNZAFBOZAPQOZFBOZPFBOZFUQUTAVABOZURUOUPUPFELZNZUTVBRZUPUOVDUOVCUPCDEF
      GIJSZTUAUOUPPMLVCVEUBAPFBCDEGHIUCUDUEUOVBURRUPUOVAFABUOVCVAFRZVFFBCDEFGHI
      JUFUGZUHUIUJUPUTVARUOUPUSPFBAUKULUMUOVGUPVHUIUN $.
      $( [5-Feb-2007] $) $( [24-Nov-2006] $)
  $}

  ${
    vcm.1 $e |- G = ( 1st ` W ) $.
    vcm.2 $e |- S = ( 2nd ` W ) $.
    vcm.3 $e |- X = ran G $.
    vcm.4 $e |- M = ( inv ` G ) $.
    $( Minus 1 times a vector is the underlying group's inverse element.
       Equation 2 of [Kreyszig] p. 51. $)
    vcm $p |- ( ( W e. CVec /\ A e. X ) -> ( -u 1 S A ) = ( M ` A ) ) $=
      ( cvc wcel wa cgi cfv co c1 cneg cgr w3a wceq grpass vcgrp adantr cc
      ax1cn negcl vccl mp3an2 pm3.27 grpinvcl sylan 3jca sylanc vcid opreq2d
      caddc cc0 addcom negid eqtr opreq1i a1i vcdir mp3anr1 mpanr1 eqid vc0
      3eqtr3d eqtr3d opreq1d grprinv grplid grprid 3eqtr3rd ) EKLZAFLZMZCNOZADO
      ZCPZQRZABPZVSCPZVTWCVRWCACPZVTCPZWCAVTCPZCPZWAWDCSLZWCFLZVQVTFLZTWFWHUAVR
      WCAVTCFIUBVPWIVQCEGUCZUDZVRWJVQWKVPWBUELZVQWJQUFUGZWBABCEFGHIUHUIZVPVQUJW
      IVQWKVPACDFIJUKWLULZUMUNVRWEVSVTCVRWCQABPZCPZWEVSVRWRAWCCABCEFGHIUOUPVRWB
      QUQPZABPZURABPZWSVSXAXBUAVRWTURABWTQWBUQPURWBQWOUFUSQUFUTVAVBVCVPQUELZVQX
      AWSUAZUFVPWNXCVQXDWOWBQABCEFGHIVDVEVFABCEFVSGHIVSVGZVHVIVJVKVRWGVSWCCWIVQ
      WGVSUAVPAVSCDFIXEJVLWLULUPVIWIWKWAVTUAVRVTVSCFIXEVMWMWQUNWIWJWDWCUAVRWCVS
      CFIXEVNWMWPUNVO $.
      $( [4-Dec-2006] $) $( [25-Nov-2006] $)
  $}

  ${
    vcrinv.1 $e |- G = ( 1st ` W ) $.
    vcrinv.2 $e |- S = ( 2nd ` W ) $.
    vcrinv.3 $e |- X = ran G $.
    vcrinv.4 $e |- Z = ( Id ` G ) $.
    $( A vector minus itself. $)
    vcrinv $p |- ( ( W e. CVec /\ A e. X ) -> ( A G ( -u 1 S A ) ) = Z ) $=
      ( cvc wcel wa c1 cneg co cgn cfv eqid vcm opreq2d cgr wceq grprinv vcgrp
      sylan eqtrd ) DKLZAELZMZANOABPZCPAACQRZRZCPZFUJUKUMACABCULDEGHIULSZTUACUB
      LUIUNFUCUHAFCULEIJUOUDCDGUEUFUG $.
      $( [14-Dec-2006] $) $( [4-Dec-2006] $)

    $( Minus a vector plus itself. $)
    vclinv $p |- ( ( W e. CVec /\ A e. X ) -> ( ( -u 1 S A ) G A ) = Z ) $=
      ( cvc wcel wa c1 cneg co cgn cfv eqid vcm opreq1d cgr wceq grplinv vcgrp
      sylan eqtrd ) DKLZAELZMZNOABPZACPACQRZRZACPZFUJUKUMACABCULDEGHIULSZTUACUB
      LUIUNFUCUHAFCULEIJUOUDCDGUEUFUG $.
      $( [6-Dec-2006] $) $( [4-Dec-2006] $)
  $}

  ${
    vcinv0.1 $e |- G = ( 1st ` W ) $.
    vcinv0.2 $e |- S = ( 2nd ` W ) $.
    vcinv0.4 $e |- Z = ( Id ` G ) $.
    $( The zero vector equals its negative. $)
    vcinv0 $p |- ( W e. CVec -> ( -u 1 S Z ) = Z ) $=
      ( cvc wcel c1 cneg cc co wceq ax1cn negcl crn eqid vcz mpan2 ) CHIJKZLIUA
      DAMDNJOPUAABCBQZDEFUBRGST $.
      $( [9-Dec-2006] $) $( [4-Dec-2006] $)
  $}

