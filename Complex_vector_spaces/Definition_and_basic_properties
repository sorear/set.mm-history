$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Definition and basic properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c CVec $.

  $( Extend class notation with the class of all complex vector spaces. $)
  cvc $a class CVec $.

  ${
    $d g s x y z $.
    $( Define the class of all complex vector spaces. $)
    df-vc $a |- CVec = { <. g , s >. | ( g e. AbelOp /\
     s : ( CC X. ran g ) --> ran g /\ A. x e. ran g ( ( 1 s x ) = x /\
       A. y e. CC ( A. z e. ran g ( y s ( x g z ) ) = ( ( y s x ) g ( y s z ) )
         /\ A. z e. CC ( ( ( y + z ) s x ) = ( ( y s x ) g ( z s x ) ) /\
           ( ( y x. z ) s x ) = ( y s ( z s x ) ) ) ) ) ) } $.

    $( The class of all complex vector spaces is a relation. $)
    vcrel $p |- Rel CVec $=
      ( vg vs vx vy vz cvc wrel cv cabl wcel cc crn wf c1 co wceq wral caddc wa
      cxp cmul w3a copab relopab df-vc releqi mpbir ) FGAHZIJKUHLZTUIBHZMNCHZUJ
      OUKPDHZUKEHZUHOUJOULUKUJOZULUMUJOUHOPEUIQULUMROUKUJOUNUMUKUJOZUHOPULUMUAO
      UKUJOULUOUJOPSEKQSDKQSCUIQUBZABUCZGUPABUDFUQCDEABUEUFUG $.
      $( [17-Mar-2007] $)
  $}

  ${
    $d g s w x y z G $.  $d g s w x y z S $.  $d g s W $.  $d g s w x y z X $.
    $d w x y z A $.  $d w x y z B $.  $d x y z C $.
    vci.1 $e |- G = ( 1st ` W ) $.
    vci.2 $e |- S = ( 2nd ` W ) $.
    vci.3 $e |- X = ran G $.
    $( The properties of a complex vector space, which is an Abelian group
       (i.e. the vectors, with the operation of vector addition) accompanied by
       a scalar multiplication operation on the field of complex numbers.  The
       variable ` W ` was chosen because ` _V ` is already used for the
       universal class. $)
    vci $p |- ( W e. CVec ->
              ( G e. AbelOp /\
         S : ( CC X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. CC ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. CC ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) $=
      ( vg vs cv cc co wceq wral wa opreq ralbidv cvc wcel cabl crn wf c1 caddc
      cxp cmul w3a copab df-vc eleq2i c1st cfv wb eqeq2i eleq1 rneq xpeq2 feq2d
      syl6eqr feq3 bitrd opreq2d eqeq12d raleqbidv eqeq2d anbi1d anbi12d anbi2d
      3anbi123d sylbir c2nd feq1 eqeq1d opreq12d eqtrd 3anbi23d elopabi sylbi
      syl ) FUAUBFKMZUCUBZNWCUDZUHZWELMZUEZUFAMZWGOZWIPZBMZWICMZWCOZWGOZWLWIWGO
      ZWLWMWGOZWCOZPZCWEQZWLWMUGOZWIWGOZWPWMWIWGOZWCOZPZWLWMUIOZWIWGOZWLXCWGOZP
      ZRZCNQZRZBNQZRZAWEQZUJZKLUKZUBEUCUBZNGUHZGDUEZUFWIDOZWIPZWLWIWMEOZDOZWLWI
      DOZWLWMDOZEOZPZCGQZXAWIDOZYEWMWIDOZEOZPZXFWIDOZWLYKDOZPZRZCNQZRZBNQZRZAGQ
      ZUJZUAXQFABCKLULUMXPXRXSGWGUEZWKWLYCWGOZWPWQEOZPZCGQZXBWPXCEOZPZXIRZCNQZR
      ZBNQZRZAGQZUJZUUCKLFWCFUNUOZPWCEPZXPUUQUPEUURWCHUQUUSWDXRWHUUDXOUUPWCEUCU
      RUUSWEGPZWHUUDUPUUSWEEUDGWCEUSJVBZUUTWHXSWEWGUEUUDUUTWFXSWEWGWEGNUTVAWEGX
      SWGVCVDWBUUSXNUUOAWEGUVAUUSXMUUNWKUUSXLUUMBNUUSWTUUHXKUULUUSWSUUGCWEGUVAU
      USWOUUEWRUUFUUSWNYCWLWGWIWMWCESVEWPWQWCESVFVGUUSXJUUKCNUUSXEUUJXIUUSXDUUI
      XBWPXCWCESVHVITVJTVKVGVLVMWGFVNUOZPWGDPZUUQUUCUPDUVBWGIUQUVCUUDXTUUPUUBXR
      XSGWGDVOUVCUUOUUAAGUVCWKYBUUNYTUVCWJYAWIUFWIWGDSVPUVCUUMYSBNUVCUUHYIUULYR
      UVCUUGYHCGUVCUUEYDUUFYGWLYCWGDSUVCWPYEWQYFEWLWIWGDSZWLWMWGDSVQVFTUVCUUKYQ
      CNUVCUUJYMXIYPUVCXBYJUUIYLXAWIWGDSUVCWPYEXCYKEUVDWMWIWGDSZVQVFUVCXGYNXHYO
      XFWIWGDSUVCXHWLYKWGOYOUVCXCYKWLWGUVEVEWLYKWGDSVRVFVJTVJTVJTVSVMVTWA $.
      $( [3-Nov-2006] $)

    $( Functionality of th scalar product of a complex vector space. $)
    vcsm $p |- ( W e. CVec -> S : ( CC X. X ) --> X ) $=
      ( vx vy vz cvc wcel cabl cc cxp cv co wceq wral wa wf c1 caddc vci simp2d
      cmul ) CKLBMLNDODAUAUBHPZAQUGRIPZUGJPZBQAQUHUGAQZUHUIAQBQRJDSUHUIUCQUGAQU
      JUIUGAQZBQRUHUIUFQUGAQUHUKAQRTJNSTINSTHDSHIJABCDEFGUDUE $.
      $( [3-Nov-2006] $)

    $( Closure of the scalar product of a complex vector space. $)
    vccl $p |- ( ( W e. CVec /\ A e. CC /\ B e. X ) -> ( A S B ) e. X ) $=
      ( cvc wcel cc cxp wf co vcsm foprrn syl3an1 ) EJKLFMFCNALKBFKABCOFKCDEFGH
      IPABFLFCQR $.
      $( [3-Nov-2006] $)

    $( Identity element for the scalar product of a complex vector space. $)
    vcid $p |- ( ( W e. CVec /\ A e. X ) -> ( 1 S A ) = A ) $=
      ( vx vy vz cvc wcel c1 cv co wceq wral cc wa cabl cxp wf caddc cmul simpl
      w3a vci ralimi 3ad2ant3 syl opreq2 id eqeq12d rcla4cva sylan ) DLMZNIOZBP
      ZURQZIERZAEMNABPZAQZUQCUAMZSEUBEBUCZUTJOZURKOZCPBPVFURBPZVFVGBPCPQKERVFVG
      UDPURBPVHVGURBPZCPQVFVGUEPURBPVFVIBPQTKSRTJSRZTZIERZUGVAIJKBCDEFGHUHVLVDV
      AVEVKUTIEUTVJUFUIUJUKUTVCIAEURAQZUSVBURAURANBULVMUMUNUOUP $.
      $( [3-Nov-2006] $)

    $( Distributive law for the scalar product of a complex vector space. $)
    vcdi $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. X /\ C e. X ) ) ->
       ( A S ( B G C ) ) = ( ( A S B ) G ( A S C ) ) ) $=
      ( vy vx vz cc wcel w3a co wceq wral opreq1 cvc wi cv cabl cxp wf c1 caddc
      cmul vci simpl ralimi adantl 3ad2ant3 syl opreq2d opreq2 opreq1d opreq12d
      wa eqeq12d rcla43v syl5 3com12 impcom ) ANOZBGOZCGOZPFUAOZABCEQZDQZABDQZA
      CDQZEQZRZVGVFVHVIVOUBVIKUCZLUCZMUCZEQZDQZVPVQDQZVPVRDQZEQZRZMGSZKNSZLGSZV
      GVFVHPVOVIEUDOZNGUEGDUFZUGVQDQVQRZWEVPVRUHQVQDQWAVRVQDQZEQRVPVRUIQVQDQVPW
      KDQRUTMNSZUTZKNSZUTZLGSZPWGLKMDEFGHIJUJWPWHWGWIWOWFLGWNWFWJWMWEKNWEWLUKUL
      UMULUNUOWDVOVPBVREQZDQZVPBDQZWBEQZRAWQDQZVLAVRDQZEQZRLKMBACGNGVQBRZVTWRWC
      WTXDVSWQVPDVQBVRETUPXDWAWSWBEVQBVPDUQURVAVPARZWRXAWTXCVPAWQDTXEWSVLWBXBEV
      PABDTVPAVRDTUSVAVRCRZXAVKXCVNXFWQVJADVRCBEUQUPXFXBVMVLEVRCADUQUPVAVBVCVDV
      E $.
      $( [3-Nov-2006] $)

    $( Distributive law for the scalar product of a complex vector space. $)
    vcdir $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A + B ) S C ) = ( ( A S C ) G ( B S C ) ) ) $=
      ( vy vz vx cc wcel caddc co wceq wral opreq2 w3a cvc wi cv cabl cxp wf c1
      cmul vci simpl ralimi adantl 3ad2ant3 syl opreq12d eqeq12d opreq1 opreq1d
      wa opreq2d rcla43v syl5 3coml impcom ) ANOZBNOZCGOZUAFUBOZABPQZCDQZACDQZB
      CDQZEQZRZVHVFVGVIVOUCVIKUDZLUDZPQZMUDZDQZVPVSDQZVQVSDQZEQZRZLNSZKNSZMGSZV
      HVFVGUAVOVIEUEOZNGUFGDUGZUHVSDQVSRZVPVSVQEQDQWAVPVQDQEQRLGSZWDVPVQUIQVSDQ
      VPWBDQRZUTZLNSZUTZKNSZUTZMGSZUAWGMKLDEFGHIJUJWRWHWGWIWQWFMGWPWFWJWOWEKNWN
      WEWKWMWDLNWDWLUKULUMULUMULUNUOWDVOVRCDQZVPCDQZVQCDQZEQZRAVQPQZCDQZVLXAEQZ
      RMKLCABGNNVSCRZVTWSWCXBVSCVRDTXFWAWTWBXAEVSCVPDTVSCVQDTUPUQVPARZWSXDXBXEX
      GVRXCCDVPAVQPURUSXGWTVLXAEVPACDURUSUQVQBRZXDVKXEVNXHXCVJCDVQBAPTUSXHXAVMV
      LEVQBCDURVAUQVBVCVDVE $.
      $( [3-Nov-2006] $)

    $( Associative law for the scalar product of a complex vector space. $)
    vcass $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A x. B ) S C ) = ( A S ( B S C ) ) ) $=
      ( vy vz vx cc wcel w3a cmul co wceq wral cvc wi cv cxp wf c1 caddc wa vci
      cabl ralimi adantl 3ad2ant3 opreq2 opreq2d eqeq12d opreq1 opreq1d rcla43v
      simpr syl syl5 3coml impcom ) ANOZBNOZCGOZPFUAOZABQRZCDRZABCDRZDRZSZVGVEV
      FVHVMUBVHKUCZLUCZQRZMUCZDRZVNVOVQDRZDRZSZLNTZKNTZMGTZVGVEVFPVMVHEUJOZNGUD
      GDUEZUFVQDRVQSZVNVQVOERDRVNVQDRZVNVODRERSLGTZVNVOUGRVQDRWHVSERSZWAUHZLNTZ
      UHZKNTZUHZMGTZPWDMKLDEFGHIJUIWPWEWDWFWOWCMGWNWCWGWMWBKNWLWBWIWKWALNWJWAUT
      UKULUKULUKUMVAWAVMVPCDRZVNVOCDRZDRZSAVOQRZCDRZAWRDRZSMKLCABGNNVQCSZVRWQVT
      WSVQCVPDUNXCVSWRVNDVQCVODUNUOUPVNASZWQXAWSXBXDVPWTCDVNAVOQUQURVNAWRDUQUPV
      OBSZXAVJXBVLXEWTVICDVOBAQUNURXEWRVKADVOBCDUQUOUPUSVBVCVD $.
      $( [3-Nov-2006] $)

    $( A vector plus itself is two times the vector. $)
    vc2 $p |- ( ( W e. CVec /\ A e. X ) -> ( A G A ) = ( 2 S A ) ) $=
      ( cvc wcel wa c1 co c2 vcid opreq12d caddc df-2 opreq1i ax-1cn wceq vcdir
      cc mp3anr1 mpanr1 syl5req eqtr3d ) DIJZAEJZKZLABMZUKCMZAACMNABMZUJUKAUKAC
      ABCDEFGHOZUNPUJUMLLQMZABMZULNUOABRSUHLUCJZUIUPULUAZTUHUQUQUIURTLLABCDEFGH
      UBUDUEUFUG $.
      $( [1-Feb-2007] $)

    $( Subtractive distributive law for the scalar product of a complex vector
       space. $)
    vcsubdir $p |- ( ( W e. CVec /\ ( A e. CC /\ B e. CC /\ C e. X ) ) ->
      ( ( A - B ) S C ) = ( ( A S C ) G ( -u 1 S ( B S C ) ) ) ) $=
      ( cvc wcel cc w3a wa cneg co c1 wceq opreq1d caddc negcl syl3anr2 3adant3
      cmin vcdir negsub adantl cmul mulm1 ad2antrl ax-1cn negcli mp3anr1 eqtr3d
      vcass 3adantr1 opreq2d 3eqtr3d ) FKLZAMLZBMLZCGLZNZOZABPZUAQZCDQZACDQZVFC
      DQZEQZABUEQZCDQVIRPZBCDQDQZEQVBVAUTVFMLVCVHVKSBUBAVFCDEFGHIJUFUCVEVGVLCDV
      DVGVLSZUTVAVBVOVCABUGUDUHTVEVJVNVIEUTVBVCVJVNSVAUTVBVCOOZVMBUIQZCDQZVJVNV
      PVQVFCDVBVQVFSUTVCBUJUKTUTVMMLVBVCVRVNSRULUMVMBCDEFGHIJUPUNUOUQURUS $.
      $( [31-Jul-2007] $)
  $}

  ${
    $d x y z G $.  $d x y z W $.
    vcabl.1 $e |- G = ( 1st ` W ) $.
    $( Vector addition is an Abelian group operation. $)
    vcabl $p |- ( W e. CVec -> G e. AbelOp ) $=
      ( vx vy vz cvc wcel cabl cc crn cxp c2nd cfv cv co wceq wral wa eqid cmul
      wf c1 caddc vci simp1d ) BGHAIHJAKZLUGBMNZUBUCDOZUHPUIQEOZUIFOZAPUHPUJUIU
      HPZUJUKUHPAPQFUGRUJUKUDPUIUHPULUKUIUHPZAPQUJUKUAPUIUHPUJUMUHPQSFJRSEJRSDU
      GRDEFUHABUGCUHTUGTUEUF $.
      $( [3-Nov-2006] $)

    $( Vector addition is a group operation. $)
    vcgrp $p |- ( W e. CVec -> G e. GrpOp ) $=
      ( cvc wcel cabl cgr vcabl ablogrpo syl ) BDEAFEAGEABCHAIJ $.
      $( [4-Nov-2006] $)
  $}

  ${
    vcgcl.1 $e |- G = ( 1st ` W ) $.
    vcgcl.2 $e |- X = ran G $.
    $( Closure law for the vector addition (group) operation of a complex
       vector space. $)
    vcgcl $p |- ( ( W e. CVec /\ A e. X /\ B e. X ) -> ( A G B ) e. X ) $=
      ( cvc wcel cgr co vcgrp grpocl syl3an1 ) DHICJIAEIBEIABCKEICDFLABCEGMN $.
      $( [1-Feb-2007] $)
  $}

  ${
    vccom.1 $e |- G = ( 1st ` W ) $.
    vccom.2 $e |- X = ran G $.
    $( Vector addition is commutative. $)
    vccom $p |- ( ( W e. CVec /\ A e. X /\ B e. X ) ->
                ( A G B ) = ( B G A ) ) $=
      ( cvc wcel cabl co wceq vcabl ablocom syl3an1 ) DHICJIAEIBEIABCKBACKLCDFM
      ABCEGNO $.
      $( [4-Nov-2006] $)

    $( Vector addition is associative. $)
    vcaass $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                ( ( A G B ) G C ) = ( A G ( B G C ) ) ) $=
      ( cvc wcel cgr w3a co wceq vcgrp grpoass sylan ) EIJDKJAFJBFJCFJLABDMCDMA
      BCDMDMNDEGOABCDFHPQ $.
      $( [4-Nov-2006] $)

    $( Commutative/associative law that swaps the last two terms in a triple
       vector sum. $)
    vca32 $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
     ( ( A G B ) G C ) = ( ( A G C ) G B ) ) $=
      ( cvc wcel cabl w3a co wceq vcabl ablo32 sylan ) EIJDKJAFJBFJCFJLABDMCDMA
      CDMBDMNDEGOABCDFHPQ $.
      $( [5-Aug-2007] $)

    $( Rearrangement of 4 terms in a vector sum. $)
    vca4 $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X )
      ) -> ( ( A G B ) G ( C G D ) ) = ( ( A G C ) G ( B G D ) ) ) $=
      ( cvc wcel cabl wa co wceq vcabl ablo4 syl3an1 ) FJKELKAGKBGKMCGKDGKMABEN
      CDENENACENBDENENOEFHPABCDEGIQR $.
      $( [5-Aug-2007] $)

    $( Right cancellation law for vector addition. $)
    vcrcan $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( A G C ) = ( B G C ) <-> A = B ) ) $=
      ( cvc wcel cgr w3a co wceq wb vcgrp grporcan sylan ) EIJDKJAFJBFJCFJLACDM
      BCDMNABNODEGPABCDFHQR $.
      $( [4-Nov-2006] $)

    $( Left cancellation law for vector addition. $)
    vclcan $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X /\ C e. X ) ) ->
                  ( ( C G A ) = ( C G B ) <-> A = B ) ) $=
      ( cvc wcel cgr w3a co wceq wb vcgrp grpolcan sylan ) EIJDKJAFJBFJCFJLCADM
      CBDMNABNODEGPABCDFHQR $.
      $( [4-Nov-2006] $)
  $}

  ${
    vczcl.1 $e |- G = ( 1st ` W ) $.
    vczcl.2 $e |- X = ran G $.
    vczcl.3 $e |- Z = ( Id ` G ) $.
    $( The zero vector is a vector. $)
    vczcl $p |- ( W e. CVec -> Z e. X ) $=
      ( cvc wcel cgr vcgrp grpoidcl syl ) BHIAJIDCIABEKDACFGLM $.
      $( [4-Nov-2006] $)

    $( The zero vector is a right identity element. $)
    vc0rid $p |- ( ( W e. CVec /\ A e. X ) -> ( A G Z ) = A ) $=
      ( cvc wcel cgr co wceq vcgrp grporid sylan ) CIJBKJADJAEBLAMBCFNAEBDGHOP
      $.
      $( [4-Nov-2006] $)

    $( The zero vector is a left identity element. $)
    vc0lid $p |- ( ( W e. CVec /\ A e. X ) -> ( Z G A ) = A ) $=
      ( cvc wcel cgr co wceq vcgrp grpolid sylan ) CIJBKJADJEABLAMBCFNAEBDGHOP
      $.
      $( [26-Apr-2007] $)
  $}

  ${
    vc0.1 $e |- G = ( 1st ` W ) $.
    vc0.2 $e |- S = ( 2nd ` W ) $.
    vc0.3 $e |- X = ran G $.
    vc0.4 $e |- Z = ( Id ` G ) $.
    $( Zero times a vector is the zero vector.  Equation 1a of [Kreyszig]
       p. 51. $)
    vc0 $p |- ( ( W e. CVec /\ A e. X ) -> ( 0 S A ) = Z ) $=
      ( cvc wcel wa cc0 co wceq c1 ax-1cn cc 0cn vc0rid caddc addid1i a1i vcdir
      opreq1i mp3anr1 mpanr1 vcid 3eqtr3d opreq1d 3eqtr2rd wb vccl mp3an2 vczcl
      w3a adantr simpr 3jca vclcan syldan mpbid ) DKLZAELZMZANABOZCOZAFCOZPZVGF
      PZVFVIAQABOZVGCOZVHACDEFGIJUAVFQNUBOZABOZVLVMAVOVLPVFVNQABQRUCUFUDVDNSLZV
      EVOVMPZTVDQSLVPVEVQRQNABCDEGHIUEUGUHABCDEGHIUIZUJVFVLAVGCVRUKULVDVEVGELZF
      ELZVEUQVJVKUMVFVSVTVEVDVPVEVSTNABCDEGHIUNUOVDVTVECDEFGIJUPURVDVEUSUTVGFAC
      DEGIVAVBVC $.
      $( [4-Nov-2006] $)

    $( Anything times the zero vector is the zero vector.  Equation 1b of
       [Kreyszig] p. 51. $)
    vcz $p |- ( ( W e. CVec /\ A e. CC ) -> ( A S Z ) = Z ) $=
      ( cvc wcel cc wa co cc0 cmul wceq vczcl adantr anim2i ancoms vcass syldan
      0cn mp3anr2 vc0 mpdan opreq2d eqtr2d mul01 opreq1d adantl 3eqtrd ) DKLZAM
      LZNZAFBOZAPQOZFBOZPFBOZFUQUTAVABOZURUOUPUPFELZNZUTVBRZUPUOVDUOVCUPCDEFGIJ
      SZUAUBUOUPPMLVCVEUEAPFBCDEGHIUCUFUDUOVBURRUPUOVAFABUOVCVAFRZVFFBCDEFGHIJU
      GUHZUITUJUPUTVARUOUPUSPFBAUKULUMUOVGUPVHTUN $.
      $( [24-Nov-2006] $)
  $}

  ${
    vcm.1 $e |- G = ( 1st ` W ) $.
    vcm.2 $e |- S = ( 2nd ` W ) $.
    vcm.3 $e |- X = ran G $.
    vcm.4 $e |- M = ( inv ` G ) $.
    $( Minus 1 times a vector is the underlying group's inverse element.
       Equation 2 of [Kreyszig] p. 51. $)
    vcm $p |- ( ( W e. CVec /\ A e. X ) -> ( -u 1 S A ) = ( M ` A ) ) $=
      ( wcel cfv co c1 wceq cc ax-1cn sylan opreq2d cc0 cvc cgi cneg cgr adantr
      wa vcgrp negcli vccl mp3an2 simpr grpoinvcl grpoass syl13anc vcid addcomi
      caddc negidi eqtri opreq1i a1i mp3anr1 mpanr1 eqid 3eqtr3d eqtr3d opreq1d
      vcdir vc0 grporinv grpolid syl2anc grporid 3eqtr3rd ) EUAKZAFKZUFZCUBLZAD
      LZCMZNUCZABMZVRCMZVSWBVQWBACMZVSCMZWBAVSCMZCMZVTWCVQCUDKZWBFKZVPVSFKZWEWG
      OVOWHVPCEGUGZUEZVOWAPKZVPWINQUHZWAABCEFGHIUIUJZVOVPUKVOWHVPWJWKACDFIJULRZ
      WBAVSCFIUMUNVQWDVRVSCVQWBNABMZCMZWDVRVQWQAWBCABCEFGHIUOSVQWANUQMZABMZTABM
      ZWRVRWTXAOVQWSTABWSNWAUQMTWANWNQUPNQURUSUTVAVONPKZVPWTWROZQVOWMXBVPXCWNWA
      NABCEFGHIVHVBVCABCEFVRGHIVRVDZVIVEVFVGVQWFVRWBCVOWHVPWFVROWKAVRCDFIXDJVJR
      SVEVQWHWJVTVSOWLWPVSVRCFIXDVKVLVQWHWIWCWBOWLWOWBVRCFIXDVMVLVN $.
      $( [25-Nov-2006] $)
  $}

  ${
    vcrinv.1 $e |- G = ( 1st ` W ) $.
    vcrinv.2 $e |- S = ( 2nd ` W ) $.
    vcrinv.3 $e |- X = ran G $.
    vcrinv.4 $e |- Z = ( Id ` G ) $.
    $( A vector minus itself. $)
    vcrinv $p |- ( ( W e. CVec /\ A e. X ) -> ( A G ( -u 1 S A ) ) = Z ) $=
      ( cvc wcel wa c1 cneg co cgn cfv eqid vcm opreq2d cgr wceq vcgrp grporinv
      sylan eqtrd ) DKLZAELZMZANOABPZCPAACQRZRZCPZFUJUKUMACABCULDEGHIULSZTUAUHC
      UBLUIUNFUCCDGUDAFCULEIJUOUEUFUG $.
      $( [4-Dec-2006] $)

    $( Minus a vector plus itself. $)
    vclinv $p |- ( ( W e. CVec /\ A e. X ) -> ( ( -u 1 S A ) G A ) = Z ) $=
      ( cvc wcel wa c1 cneg co cgn cfv eqid vcm opreq1d cgr wceq vcgrp grpolinv
      sylan eqtrd ) DKLZAELZMZNOABPZACPACQRZRZACPZFUJUKUMACABCULDEGHIULSZTUAUHC
      UBLUIUNFUCCDGUDAFCULEIJUOUEUFUG $.
      $( [4-Dec-2006] $)
  $}

  ${
    vcni.1 $e |- G = ( 1st ` W ) $.
    vcni.2 $e |- S = ( 2nd ` W ) $.
    vcni.3 $e |- X = ran G $.
    $( Double negative of a vector. $)
    vcnegneg $p |- ( ( W e. CVec /\ A e. X ) ->
                  ( -u 1 S ( -u 1 S A ) ) = A ) $=
      ( cvc wcel wa c1 cneg cmul co cc wceq ax-1cn negcli vcass mpanr1 mul2negi
      mp3anr1 mulid1i eqtri opreq1i vcid syl5eq eqtr3d ) DIJZAEJZKZLMZUMNOZABOZ
      UMUMABOBOZAUJUMPJZUKUOUPQZLRSZUJUQUQUKURUSUMUMABCDEFGHTUCUAULUOLABOAUNLAB
      UNLLNOLLLRRUBLRUDUEUFABCDEFGHUGUHUI $.
      $( [6-Aug-2007] $)

    $( Distribution of negative over vector subtraction. $)
    vcnegsubdi2 $p |- ( ( W e. CVec /\ A e. X /\ B e. X ) ->
              ( -u 1 S ( A G ( -u 1 S B ) ) ) = ( B G ( -u 1 S A ) ) ) $=
      ( cvc wcel w3a c1 cneg co wceq cc vccl mp3an2 3adant2 ax-1cn vcdi mp3anr1
      negcli 3impb syld3an3 vcnegneg opreq2d 3adant3 vccom syld3an2 3eqtrd ) EJ
      KZAFKZBFKZLZMNZAUQBCOZDOCOZUQACOZUQURCOZDOZUTBDOZBUTDOZUMUNUOURFKZUSVBPZU
      MUOVEUNUMUQQKZUOVEMUAUDZUQBCDEFGHIRSTUMUNVEVFUMVGUNVEVFVHUQAURCDEFGHIUBUC
      UEUFUPVABUTDUMUOVABPUNBCDEFGHIUGTUHUMUTFKZUNUOVCVDPUMUNVIUOUMVGUNVIVHUQAC
      DEFGHIRSUIUTBDEFGIUJUKUL $.
      $( [6-Aug-2007] $)

    $( Rearrangement of 4 terms in a mixed vector addition and subtraction. $)
    vcsub4 $p |- ( ( W e. CVec /\ ( A e. X /\ B e. X ) /\ ( C e. X /\ D e. X )
              ) -> ( ( A G B ) G ( -u 1 S ( C G D ) ) ) =
            ( ( A G ( -u 1 S C ) ) G ( B G ( -u 1 S D ) ) ) ) $=
      ( cvc wcel wa co c1 wceq 3adant2 vccl mp3an2 w3a cc ax-1cn negcli mp3anr1
      cneg vcdi opreq2d anim12i anandis vca4 syld3an3 eqtrd ) GLMZAHMBHMNZCHMZD
      HMZNZUAZABFOZPUFZCDFOEOZFOUTVACEOZVADEOZFOZFOZAVCFOBVDFOFOZUSVBVEUTFUNURV
      BVEQZUOUNVAUBMZUPUQVHPUCUDZVACDEFGHIJKUGUERUHUNUOURVCHMZVDHMZNZVFVGQUNURV
      MUOUNUPUQVMUNUPNVKUNUQNVLUNVIUPVKVJVACEFGHIJKSTUNVIUQVLVJVADEFGHIJKSTUIUJ
      RABVCVDFGHIKUKULUM $.
      $( [5-Aug-2007] $)
  $}

  ${
    $d g s x y z G $.  $d g s x y z S $.  $d g s x z X $.
    isvclem.1 $e |- X = ran G $.
    $( Lemma for ~ isvc . $)
    isvclem $p |- ( ( G e. _V /\ S e. _V ) -> ( <. G , S >. e. CVec
     <-> ( G e. AbelOp /\ S : ( CC X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. CC ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. CC ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) ) $=
      ( vg vs wcel cv cc wf co wceq wral wa cvv opreq ralbidv cop cvc crn caddc
      cabl cxp c1 cmul w3a copab df-vc eleq2i eleq1 wb rneq syl6eqr xpeq2 feq2d
      feq3 syl opreq2d eqeq12d raleqbidv eqeq2d anbi1d anbi12d anbi2d 3anbi123d
      bitrd feq1 eqeq1d opreq12d eqtrd 3anbi23d opelopabg syl5bb ) EDUAZUBJVQHK
      ZUEJZLVRUCZUFZVTIKZMZUGAKZWBNZWDOZBKZWDCKZVRNZWBNZWGWDWBNZWGWHWBNZVRNZOZC
      VTPZWGWHUDNZWDWBNZWKWHWDWBNZVRNZOZWGWHUHNZWDWBNZWGWRWBNZOZQZCLPZQZBLPZQZA
      VTPZUIZHIUJZJERJDRJQEUEJZLFUFZFDMZUGWDDNZWDOZWGWDWHENZDNZWGWDDNZWGWHDNZEN
      ZOZCFPZWPWDDNZXTWHWDDNZENZOZXAWDDNZWGYFDNZOZQZCLPZQZBLPZQZAFPZUIZUBXLVQAB
      CHIUKULXKXMXNFWBMZWFWGXRWBNZWKWLENZOZCFPZWQWKWRENZOZXDQZCLPZQZBLPZQZAFPZU
      IYRHIEDRRVREOZVSXMWCYSXJUUKVREUEUMUULVTFOZWCYSUNUULVTEUCFVREUOGUPZUUMWCXN
      VTWBMYSUUMWAXNVTWBVTFLUQURVTFXNWBUSVIUTUULXIUUJAVTFUUNUULXHUUIWFUULXGUUHB
      LUULWOUUCXFUUGUULWNUUBCVTFUUNUULWJYTWMUUAUULWIXRWGWBWDWHVRESVAWKWLVRESVBV
      CUULXEUUFCLUULWTUUEXDUULWSUUDWQWKWRVRESVDVETVFTVGVCVHWBDOZYSXOUUKYQXMXNFW
      BDVJUUOUUJYPAFUUOWFXQUUIYOUUOWEXPWDUGWDWBDSVKUUOUUHYNBLUUOUUCYDUUGYMUUOUU
      BYCCFUUOYTXSUUAYBWGXRWBDSUUOWKXTWLYAEWGWDWBDSZWGWHWBDSVLVBTUUOUUFYLCLUUOU
      UEYHXDYKUUOWQYEUUDYGWPWDWBDSUUOWKXTWRYFEUUPWHWDWBDSZVLVBUUOXBYIXCYJXAWDWB
      DSUUOXCWGWRDNYJWGWRWBDSUUOWRYFWGDUUQVAVMVBVFTVFTVFTVNVOVP $.
      $( [31-May-2008] $)
  $}

  ${
    $d x y z G $.
    $( Lemma for ~ vcoprne . $)
    vcoprnelem $p |- ( <. G , G >. e. CVec -> G : ( CC X. CC ) --> CC ) $=
      ( vx vy vz cvv wcel cop cvc cc cxp wf syl wa cv co wceq wral wb wfn mpbid
      wrel wss vcrel df-rel mpbi sseli opelxp1 cabl crn caddc cmul eqid isvclem
      w3a anidms biimpa simpr cgr ablogrpo wfo grpofo fofn ffn fndmu syl2an wne
      c1 c0 grpon0 xpcan2 adantr sylan xpeq2 feq2d feq3 bitrd 3adant3 mpancom )
      AEFZAAGZHFZIIJZIAKZWAVTEEJZFVSHWDVTHUAHWDUBUCHUDUEUFAAEEUGLVSWAMAUHFZIAUI
      ZJZWFAKZVGBNZAOWIPCNZWIDNZAOAOWJWIAOZWJWKAOAOPDWFQWJWKUJOWIAOWLWKWIAOZAOP
      WJWKUKOWIAOWJWMAOPMDIQMCIQMBWFQZUNZWCVSWAWOVSWAWORBCDAAWFWFULZUMUOUPWEWHW
      CWNWEWHMZWHWCWEWHUQWQWFIPZWHWCRWEAURFZWHWRAUSWSWHMWFWFJZWGPZWRWSAWTSZAWGS
      XAWHWSWTWFAUTXBAWFWPVAWTWFAVBLWGWFAVCWTWGAVDVEWSXAWRRZWHWSWFVHVFXCAWFWPVI
      WFIWFVJLVKTVLWRWHWBWFAKWCWRWGWBWFAWFIIVMVNWFIWBAVOVPLTVQLVR $.
      $( [31-May-2008] $)
  $}

  $( The operations of a complex vector space cannot be identical. $)
  vcoprne $p |- ( <. G , S >. e. CVec -> G =/= S ) $=
    ( cop cvc wcel wceq c1 cc0 co cfv cvv cc cnex syl opreqd crn cdm eqid mpdan
    cgr wne wn ax-1ne0 df-ne mpbi c1st cgi cxp wf vcoprnelem fex sylancl op1stg
    xpex ax-1cn rneqd vcgrp eqeltrrd grporndm fdm dmeqd dmxpid syl6eq syl5eleqr
    c2nd 3eqtrd vc0rid vcid op2ndg anidms eqtr4d 3eqtr2d w3a vczcl vclcan mpbid
    wb 3jca opreq2d eqtr2d 0cn vcz mpan2 eqtrd 3eqtr3d mto opeq2 mtbii necon2ai
    eleq1d ) BACZDEZBABAFZBBCZDEZWLWOGHFZGHUAWPUBUCGHUDUEWOHGBIZHWNUFJZUGJZWRIZ
    GHWOWTHWSBIZWQWOWRBHWSWOBKEZWRBFWOLLUHZLBUIZXCKEXBBUJZLLMMUNXCLKBUKULZBBKUM
    NZOWOWSGHBWOGWSWRIZGGWRIZFZWSGFZWOXHGGGWNVEJZIZXIWOGWRPZEZXHGFWOGLXNUOWOXNB
    PZBQZQZLWOWRBXGUPWOBTEXPXRFWOWRBTXGWRWNWRRZUQURBUSNWOXRXCQLWOXQXCWOXDXQXCFX
    EXCLBUTNVALVBVCVFZVDZGWRWNXNWSXSXNRZWSRZVGSWOXOXMGFYAGXLWRWNXNXSXLRZYBVHSWO
    XLWRGGWOXLBWRWOXBXLBFZXFXBYEBBKKVIVJNZXGVKOVLWOWSXNEZXOXOVMXJXKVQWOYGXOXOWR
    WNXNWSXSYBYCVNYAYAVRWSGGWRWNXNXSYBVOSVPZVSZVTWOHWSXLIZWSWQGWOHLEYJWSFWAHXLW
    RWNXNWSXSYDYBYCWBWCWOYJXAWQWOXLBHWSYFOYIWDYHWEWOHXNEWTHFWOHLXNWAXTVDHWRWNXN
    WSXSYBYCVGSWEWFWMWNWKDBABWGWJWHWI $.
    $( [31-May-2008] $)

  $( The components of a complex vector space are sets. $)
  vcex $p |- ( <. G , S >. e. CVec -> ( G e. _V /\ S e. _V ) ) $=
    ( cop cvc wcel cvv cxp wrel wss vcrel df-rel mpbi sseli opelxp1 syl wn wceq
    eqid wne vcoprne df-ne sylib mt2 opprc2 eleq1d mtbiri con4i jca ) BACZDEZBF
    EZAFEZUJUIFFGZEUKDUMUIDHDUMIJDKLMBAFFNOULUJULPZUJBBCZDEZUPBBQZBRUPBBSUQPBBT
    BBUAUBUCUNUIUODBAUDUEUFUGUH $.
    $( [31-May-2008] $)

  ${
    $d x y z G $.  $d x y z S $.  $d x z X $.
    isvc.1 $e |- X = ran G $.
    $( The predicate "is a complex vector space." $)
    isvc $p |- ( <. G , S >. e. CVec <-> ( G e. AbelOp /\
         S : ( CC X. X ) --> X /\ A. x e. X ( ( 1 S x ) = x /\
       A. y e. CC ( A. z e. X ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) )
         /\ A. z e. CC ( ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) /\
           ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) ) ) ) ) $=
      ( cop cvc wcel cvv wa cabl cc cxp cv co wceq wral cgr wf c1 cmul w3a vcex
      caddc elex adantr cnex ablogrpo crn rnexg syl5eqel syl sylancr fex sylan2
      xpexg ancoms jca 3adant3 isvclem pm5.21nii ) EDHIJEKJZDKJZLZEMJZNFOZFDUAZ
      UBAPZDQVJRBPZVJCPZEQDQVKVJDQZVKVLDQEQRCFSVKVLUFQVJDQVMVLVJDQZEQRVKVLUCQVJ
      DQVKVNDQRLCNSLBNSLAFSZUDDEUEVGVIVFVOVGVILVDVEVGVDVIEMUGUHVIVGVEVGVIVHKJZV
      EVGNKJFKJZVPUIVGETJZVQEUJVRFEUKKGETULUMUNNFKKURUOVHFKDUPUQUSUTVAABCDEFGVB
      VC $.
      $( [31-May-2008] $)
  $}

  ${
    $d x y z G $.  $d x y z S $.  $d x y z X $.
    isvci.1 $e |- G e. AbelOp $.
    isvci.2 $e |- dom G = ( X X. X ) $.
    isvci.3 $e |- S : ( CC X. X ) --> X $.
    isvci.4 $e |- ( x e. X -> ( 1 S x ) = x ) $.
    isvci.5 $e |- ( ( y e. CC /\ x e. X /\ z e. X ) ->
                  ( y S ( x G z ) ) = ( ( y S x ) G ( y S z ) ) ) $.
    isvci.6 $e |- ( ( y e. CC /\ z e. CC /\ x e. X ) ->
                  ( ( y + z ) S x ) = ( ( y S x ) G ( z S x ) ) ) $.
    isvci.7 $e |- ( ( y e. CC /\ z e. CC /\ x e. X ) ->
                  ( ( y x. z ) S x ) = ( y S ( z S x ) ) ) $.
    isvci.8 $e |- W = <. G , S >. $.
    $( Properties that determine a complex vector space. $)
    isvci $p |- W e. CVec $=
      ( wcel cc co wceq wral cop cvc cabl wf c1 cv caddc cmul wa ablogrpo ax-mp
      cxp cgr grporn isvc 3com12 3expa ralrimiva w3a jca 3comr mpbir3an eqeltri
      rgen ) FEDUAZUBOVEUBPEUCPZQGULGDUDUEAUFZDRVGSZBUFZVGCUFZERDRVIVGDRZVIVJDR
      ERSZCGTZVIVJUGRVGDRVKVJVGDRZERSZVIVJUHRVGDRVIVNDRSZUIZCQTZUIZBQTZUIZAGTAB
      CDEGEGVFEUMPHEUJUKIUNUOHJWAAGVGGPZVHVTKWBVSBQWBVIQPZUIZVMVRWDVLCGWBWCVJGP
      ZVLWCWBWEVLLUPUQURWDVQCQWBWCVJQPZVQWCWFWBVQWCWFWBUSVOVPMNUTVAUQURUTURUTVD
      VBVC $.
      $( [10-Apr-2008] $) $( [5-Nov-2006] $)
  $}

