$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Real and complex numbers (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Quotient of two negatives.  (Contributed by Paul Chapman, 10-Nov-2012.) $)
  div2neg $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( -u A / -u B ) = ( A / B ) ) $=
    ( cc wcel cc0 wne w3a cneg cdiv co wceq cmul c1 negcl 3ad2ant2 simp1 simp2
    simp3 div12 syl112anc divneg syl111anc divid 3adant1 negeqd eqtr3d opreq2d
    ax-1cn negcli mulcom mpan2 mulm1 eqtrd 3ad2ant1 3eqtrd wb divcl negeq0
    necon3bid biimpa divmul mpbird ) ACDZBCDZBEFZGZAHZBHZIJABIJZKZVHVILJZVGKZVF
    VKAVHBIJZLJZAMHZLJZVGVFVHCDZVCVDVEVKVNKVDVCVQVEBNOZVCVDVEPVCVDVEQZVCVDVERZV
    HABSTVFVMVOALVFBBIJZHZVMVOVFVDVDVEWBVMKVSVSVTBBUAUBVFWAMVDVEWAMKVCBUCUDUEUF
    UGVCVDVPVGKVEVCVPVOALJZVGVCVOCDVPWCKMUHUIAVOUJUKAULUMUNUOVFVGCDZVICDVQVHEFZ
    VJVLUPVCVDWDVEANUNABUQVRVDVEWEVCVDVEWEVDBEVHEBURUSUTUDVGVIVHVATVB $.
    $( [10-Nov-2012] $)

  ${
    $d k m $.
    $( Utility lemma to convert between ` m <_ k ` and ` k e. ( ZZ>= `` m ) `
       in limit theorems.  (Contributed by Paul Chapman, 10-Nov-2012.) $)
    climuzcnv $p |- ( m e. NN -> ( ( k e. ( ZZ>= ` m ) -> ph ) <->
                                   ( k e. NN -> ( m <_ k -> ph ) ) ) ) $=
      ( cv cn wcel cuz cfv wi cle wbr wa c1 elnnuz uztrn sylan2b sylibr expcom
      eluzle a1i jcad cz nnz w3a eluz2 biimpri syl3an1 syl3an2 3expib impbid
      imbi1d impexp syl6bb ) CDZEFZBDZUNGHFZAIUPEFZUNUPJKZLZAIURUSAIIUOUQUTAUOU
      QUTUOUQURUSUQUOURUQUOLUPMGHZFZURUOUQUNVAFVBUNNUNUPMOPUPNQRUQUSIUOUNUPSTUA
      UOURUSUQURUOUPUBFZUSUQUPUCUOUNUBFZVCUSUQUNUCUQVDVCUSUDUNUPUEUFUGUHUIUJUKU
      RUSAULUM $.
      $( [10-Nov-2012] $)
  $}

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem1 $p |- ( A e. ( 0 (,] 1 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( cc0 c1 cioc co wcel c3 cexp cdiv cmin csin cfv clt wbr wa c2 cle
    sin01bnd crp wi cr w3a wb 0re 1re elioc2 mp2an simp1bi simp2bi elrp
    sylanbrc rpre cn0 cn 3nn nnnn0 ax-mp reexpcl mpan2 wne 3re nnne0i redivcl
    mp3an23 3syl resubcl syl11anc resincl syl ltle rpgt0 lediv1 syl112anc cc
    wceq rpcn recn rpne0 divsubdir divid nncn pm3.2i divdiv23 mp3an3 syl12anc
    exp1 opreq2d caddc df-3 eqcomi ax-1cn 2cn subadd2i mpbir opreq2i 1nn0
    nnge1 expsub syl5reqr eqtr3d opreq1d opreq12d eqtrd breq1d bitrd sylibd
    breq2d anim12d mpd ) ABCDEFZAAGHEZGIEZJEZAKLZMNZXNAMNZOZCAPHEZGIEZJEZXNAIEZ
    QNZYACQNZOZARXJASFZXQYDTXJAUAFZBAMNZYEXJYFYGACQNZBUAFCUAFXJYFYGYHUBUCUDUEBC
    AUFUGZUHXJYFYGYHYIUIAUJUKYEXOYBXPYCYEXOXMXNQNZYBYEXMUAFZXNUAFZXOYJTYEYFXLUA
    FZYKAULZYEYFXKUAFZYMYNYFGUMFZYOGUNFZYPUOGUPUQZAGURUSZYOGUAFGBUTZYMVAGUOVBZX
    KGVCVDVEZAXLVFVGZYEYFYLYNAVHVIZXMXNVJVGYEYJXMAIEZYAQNZYBYEYKYLYFYGYJUUFUCUU
    CUUDYNAVKZXMXNAVLVMYEUUEXTYAQYEUUEAAIEZXLAIEZJEZXTYEAVNFZXLVNFZUUKABUTZUUEU
    UJVOAVPZYEYMUULUUBXLVQVIUUNAVRZAXLAVSVMYEUUHCUUIXSJYEUUKUUMUUHCVOUUNUUOAVTV
    GZYEXKAIEZGIEZUUIXSYEXKVNFZUUKUUMUURUUIVOZYEYFYOUUSYNYSXKVQVEUUNUUOUUSUUKUU
    MOZGVNFZYTOUUTUVBYTYQUVBUOGWAUQZUUAWBXKAGWCWDWEYEUUQXRGIYEXKACHEZIEZUUQXRYE
    UVDAXKIYEUUKUVDAVOUUNAWFVIWGYEXRAGCJEZHEZUVEUVFPAHUVFPVOPCWHEZGVOGUVHWIWJGC
    PUVCWKWLWMWNWOYEUUKUUMUVGUVEVOZUUNUUOUVAYPCUMFZUVIYRWPUVAYPUVJUBCGQNZUVIYQU
    VKUOGWQUQAGCWRUSVDVGWSWTXAWTXBXCXDXEXFYEXPXNAQNZYCYEYLYFXPUVLTUUDYNXNAVJVGY
    EUVLYAUUHQNZYCYEYLYFYFYGUVLUVMUCUUDYNYNUUGXNAAVLVMYEUUHCYAQUUPXGXEXFXHVIXI
    $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem2 $p |- ( A e. ( -u 1 [,) 0 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( c1 cneg cc0 cico co wcel c2 cexp c3 cdiv cmin csin cfv cle wbr wa cr clt
    w3a cioc wb 1re renegcli 0re elico2 mp2an renegcl 3ad2ant1 lt0neg1 biimpa
    3adant2 lenegcon1 mpan2 biimpar 3adant3 3jca sylbi elioc2 biimpri
    sindivcvglem1 3syl cc wne simp1bi recn syl simp3bi gt0ne0 sylan syldan
    negeq0 necon3bid adantr mpbird syl11anc wceq sqneg opreq1d opreq2d sinneg
    sincl div2neg syl3an1 3anidm12 eqtrd breq12d breq1d anbi12d mpbid ) ABCZDEF
    GZBACZHIFZJKFZLFZWMMNZWMKFZOPZWRBOPZQZBAHIFZJKFZLFZAMNZAKFZOPZXFBOPZQZWLWMR
    GZDWMSPZWMBOPZTZWMDBUAFGZXAWLARGZWKAOPZADSPZTZXMWKRGDRGZWLXRUBBUCUDUEWKDAUF
    UGZXRXJXKXLXOXPXJXQAUHZUIXOXQXKXPXOXQXKAUJUKZULXOXPXLXQXOXLXPXOBRGZXLXPUBUC
    ABUMUNUOUPUQURXNXMXSYCXNXMUBUEUCDBWMUSUGUTWMVAVBWLAVCGZADVDZXAXIUBWLXOYDWLX
    OXPXQXTVEZAVFZVGWLXOXQYEYFWLXOXPXQXTVHXOXQQYEWMDVDZXOXQXKYHYBXOXJXKYHYAWMVI
    VJVKXOYEYHUBZXQXOYDYIYGYDADWMDAVLVMVGVNVOVPYDYEQZWSXGWTXHYJWPXDWRXFOYDWPXDV
    QYEYDWOXCBLYDWNXBJKAVRVSVTVNYJWRXECZWMKFZXFYDWRYLVQYEYDWQYKWMKAWAVSVNYDYEYL
    XFVQZYDXEVCGYDYEYMAWBXEAWCWDWEWFZWGYJWRXFBOYNWHWIVPWJ $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem3 $p |- ( ( A e. RR /\ ( abs ` A ) < 1 /\ A =/= 0 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( cr wcel cabs cfv c1 clt wbr cc0 wne w3a cneg cioo co csn cdif c2 cexp c3
    cdiv cmin csin cle wa wb 1re abslt mpan2 pm5.32i cxr renegcli rexr ax-mp
    elioo2 mp2an biimpri 3expb sylbi anim1i 3impa eldifsn sylibr wo cun c0
    difid uneq2i cin wceq wn 0re ltnr simp3bi simp2bi jaoi mto elun mtbir
    disjsn mpbir undif4 un0 3eqtr3i un23 lt01 lt0neg2 biimpa ioojoin mpanr12
    mp3an eqtr3i difeq1i eleq2i bitr3i cico simp1 wi ltle mpan imp 3adant3
    simp3 3jca elico2 sindivcvglem2 syl cioc simp2 3adant2 elioc2
    sindivcvglem1 ) ABCZADEFGHZAIJZKZAFLZFMNZIOZPZCZFAQRNSTNUANAUBEATNZUCHYAFUC
    HUDZXOAXQCZXNUDZXTXLXMXNYDXLXMUDZYCXNYEXLXPAGHZAFGHZUDZUDYCXLXMYHXLFBCZXMYH
    UEUFAFUGUHUIXLYFYGYCYCXLYFYGKZXPUJCZFUJCZYCYJUEXPBCZYKFUFUKZXPULUMZYIYLUFFU
    LUMZXPFAUNUOUPUQURUSUTAXQIVAVBXTAXPIMNZCZAIFMNZCZVCZYBXTAYQYSVDZCUUAUUBXSAU
    UBXRVDZXRPZUUBXSUUBXRXRPZVDZUUBVEVDUUDUUBUUEVEUUBXRVFVGUUBXRVHVEVIZUUFUUDVI
    UUGIUUBCZVJUUHIYQCZIYSCZVCZUUKIIGHZIBCZUULVJVKIVLUMUUIUULUUJUUIUUMXPIGHZUUL
    YKIUJCZUUIUUMUUNUULKUEYOUUMUUOVKIULUMZXPIIUNUOVMUUJUUMUULIFGHZUUOYLUUJUUMUU
    LUUQKUEUUPYPIFIUNUOVNVOVPIYQYSVQVRUUBIVSVTUUBXRXRWAUMUUBWBWCUUCXQXRYQXRVDYS
    VDZUUCXQYQXRYSWDYMUUMYIUURXQVIZYNVKUFYMUUMYIKUUNUUQUUSYIUUQUUNUFWEYIUUQUUNF
    WFWGUOWEXPIFWHWIWJWKWLWKWMAYQYSVQWNYRYBYTYRAXPIWONCZYBYRXLXPAUCHZAIGHZKZUUT
    YRXLYFUVBKZUVCYKUUOYRUVDUEYOUUPXPIAUNUOUVDXLUVAUVBXLYFUVBWPXLYFUVAUVBXLYFUV
    AYMXLYFUVAWQYNXPAWRWSWTXAXLYFUVBXBXCURYMUUMUUTUVCUEYNVKXPIAXDUOVBAXEXFYTAIF
    XGNCZYBYTXLIAGHZAFUCHZKZUVEYTXLUVFYGKZUVHUUOYLYTUVIUEUUPYPIFAUNUOUVIXLUVFUV
    GXLUVFYGWPXLUVFYGXHXLYGUVGUVFXLYGUVGXLYIYGUVGWQUFAFWRUHWTXIXCURUUMYIUVEUVHU
    EVKUFIFAXJUOVBAXKXFVOURXF $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem4 $p |- ( ( A e. CC /\ ( B e. RR /\ 0 < B ) ) ->
      ( ( abs ` ( ( 1 - ( ( A ^ 2 ) / 3 ) ) - 1 ) ) < B <->
        ( abs ` A ) < ( sqr ` ( 3 x. B ) ) ) ) $=
    ( cr wcel cc0 clt wbr wa cc cle c1 c2 cexp co c3 cdiv cmin cabs cfv cmul
    csqr wb wi 0re ltle mpan imdistani cneg caddc wceq sqcl wne 3nn nncni
    nnne0i divcl mp3an23 ax-1cn negsub 3syl opreq1d negcl pncan2 sylancr
    eqtr3d fveq2d absneg eqtrd breq1d adantr abscl absge0 jca 3re remulcl
    nngt0i ltleii mulge0 mpanl12 resqrcl syl11anc sqrge0 lt2sq syl2an cn0 2nn0
    absexp mpan2 eqcomd sqsqr breqan12d bitrd syl pm3.2i ltdiv1 mp3an3 absdiv
    absid mp2an opreq2i syl6eq recn divcan3 3bitrd bitr4d sylan2 ) BCDZEBFGZHAI
    DZXGEBJGZHZKALMNZOPNZQNZKQNZRSZBFGZARSZOBTNZUASZFGZUBXGXHXJECDXGXHXJUCUDEBU
    EUFUGXIXKHZXQXMRSZBFGZYAXIXQYDUBXKXIXPYCBFXIXPXMUHZRSZYCXIXOYERXIKYEUINZKQN
    ZXOYEXIYGXNKQXIXLIDZXMIDZYGXNUJZAUKZYIOIDZOEULZYJOUMUNZOUMUOZXLOUPUQZKIDZYJ
    YKURKXMUSUFUTVAXIYRYEIDZYHYEUJURXIYIYJYSYLYQXMVBUTKYEVCVDVEVFXIYIYJYFYCUJYL
    YQXMVGUTVHVIVJYBYAXLRSZXSFGZYTOPNZXSOPNZFGZYDYBYAXRLMNZXTLMNZFGZUUAXIXRCDZE
    XRJGZHXTCDZEXTJGZHYAUUGUBXKXIUUHUUIAVKAVLVMXKUUJUUKXKXSCDZEXSJGZUUJXGUULXJO
    CDZXGUULVNOBVOUFVJZUUNEOJGZXKUUMVNEOUDVNOUMVPZVQZOBVRVSZXSVTWAXKUULUUMUUKUU
    OUUSXSWBWAVMXRXTWCWDXIXKUUEYTUUFXSFXIYTUUEXILWEDYTUUEUJWFALWGWHWIXKUULUUMUU
    FXSUJUUOUUSXSWJWAWKWLXIYTCDZUULUUAUUDUBZXKXIYIUUTYLXLVKWMUUOUUTUULUUNEOFGZH
    UVAUUNUVBVNUUQWNYTXSOWOWPWDXIXKUUBYCUUCBFXIYCUUBXIYCYTORSZPNZUUBXIYIYCUVDUJ
    ZYLYIYMYNUVEYOYPXLOWQUQWMUVCOYTPUUNUUPUVCOUJVNUUROWRWSWTXAWIXKBIDZUUCBUJZXG
    UVFXJBXBVJUVFYMYNUVGYOYPBOXCUQWMWKXDXEXF $.
    $( [10-Nov-2012] $)

  ${
    $d a b $.
    sindivcvglem5.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem5 $p |- G : RR --> RR $=
      ( cr c1 cv c2 cexp co c3 cdiv cmin wcel resqcl cc0 wne 3re 3nn nnne0i
      redivcl mp3an23 1re resubcl mpan 3syl fopab ) BCEEFBGZHIJZKLJZMJZADUHENUI
      ENZUJENZUKENZUHOULKENKPQUMRKSTUIKUAUBFENUMUNUCFUJUDUEUFUG $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A a b $.  $d a z $.
    sindivcvglem6.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    sindivcvglem6.2 $e |-
      H = { <. a , b >. | ( a e. A /\ b = ( ( sin ` a ) / a ) ) } $.
    sindivcvglem6.3 $e |- A = { z e. RR | z =/= 0 } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem6 $p |- H : A --> RR $=
      ( cr cv csin cfv cdiv co wcel cc0 wne wa neeq1 elrab2 resincl redivcl
      syl3an1 3anidm12 sylbi fopab ) EFBJEKZLMZUHNOZDHUHBPUHJPZUHQRZSUJJPZAKZQR
      ULAUHJBUNUHQTIUAUKULUMUKUIJPUKULUMUHUBUIUHUCUDUEUFUG $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d F a b k $.  $d F j k x y $.  $d G j k y $.
    sindivcvglem7.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem7 $p |- ( ( F : NN --> RR /\ F ~~> 0 ) -> ( G o. F ) ~~> 1 ) $=
      ( vy vj vk vx cn cr wf cc0 cli wbr wa cv clt cle ccom cfv c1 cmin co
      cabs wi wral wrex wcel c3 cmul csqr cc wss ax-resscn fss mpan2 3re
      remulcl mpan adantr 3nn nngt0i mulgt0 mpanl12 0re ltle sylancr mpd
      resqrcl syl11anc sqrgt0 wb 0cn climfnn biimpa wceq breq2 imbi2d ralbidv
      rexbidv imbi12d rcla4v syl5 com23 imp impcom sylanl1 c2 cexp cdiv
      ffvelrn recn syl subid1 fveq2d breq1d sindivcvglem4 bitr4d sylan wfun
      sindivcvglem5 ffun ax-mp fvco3 mp3an1 opreq1 opreq1d opreq2d oprex
      fvopab4 eqtrd expcom exp3a adantlr pm5.74d ralbidv2 mpbid anassrs ex
      r19.21aiva fco sylancl ax-1cn biimprd ) JKALZAMNOZPZMFQZROZGQHQZSOZYKBATZ
      UAZUBUCUDZUEUAZYIROZUFZHJUGZGJUHZUFZFKUGZYMUBNOZYHUUAFKYHYIKUIZPYJYTYHUUD
      YJYTYHUUDYJPZPZYLYKAUAZMUCUDZUEUAZUJYIUKUDZULUAZROZUFZHJUGZGJUHZYTYFJUMAL
      ZYGUUEUUOYFKUMUNZUUPUOJKUMAUPUQUUEUUPYGPZUUOUUEUUKKUIZMUUKROZUURUUOUFZUUE
      UUJKUIZMUUJSOZUUSUUDUVBYJUJKUIZUUDUVBURUJYIUSUTVAZUUEMUUJROZUVCUVDMUJROUU
      EUVFURUJVBVCUJYIVDVEZUUEMKUIUVBUVFUVCUFVFUVEMUUJVGVHVIUUJVJVKUUEUVBUVFUUT
      UVEUVGUUJVLVKUUSUUTUVAUUSUURUUTUUOUURMIQZROZYLUUIUVHROZUFZHJUGZGJUHZUFZIK
      UGZUUSUUTUUOUFZUUPYGUVOUUPMUMUIYGUVOVMVNIMGHAVOUQVPUVNUVPIUUKKUVHUUKVQZUV
      IUUTUVMUUOUVHUUKMRVRUVQUVLUUNGJUVQUVKUUMHJUVQUVJUULYLUVHUUKUUIRVRVSVTWAWB
      WCWDWEWFVKWGWHUUFUUNYSGJUUFUUMYRHJJUUFYKJUIZUUMYRYFUUEUVRUUMYRVMZUFZYGUUE
      YFUVTUUEYFUVRUVSYFUVRPZUUEUVSUWAUUEPZUULYQYLUWBUULUBUUGWIWJUDZUJWKUDZUCUD
      ZUBUCUDZUEUAZYIROZYQUWAUUGUMUIZUUEUULUWHVMUWAUUGKUIZUWIJKYKAWLZUUGWMWNUWI
      UUEPUULUUGUEUAZUUKROZUWHUWIUULUWMVMUUEUWIUUIUWLUUKRUWIUUHUUGUEUUGWOWPWQVA
      UUGYIWRWSWTUWAYQUWHVMUUEUWAYPUWGYIRUWAYOUWFUEUWAYNUWEUBUCUWAYNUUGBUAZUWEB
      XAZYFUVRYNUWNVQKKBLZUWOBCDEXBZKKBXCXDJKYKBAXEXFUWAUWJUWNUWEVQUWKCDUUGUBCQ
      ZWIWJUDZUJWKUDZUCUDUWEKBUWRUUGVQZUWTUWDUBUCUXAUWSUWCUJWKUWRUUGWIWJXGXHXIE
      UBUWDUCXJXKWNXLXHWPWQVAWSVSXMXNWGXOXPXQWAXRXSXTYAYFUUBUUCUFYGYFUUCUUBYFJU
      MYMLZUBUMUIUUCUUBVMYFJKYMLZUUQUXBUWPYFUXCUWQJKKBAYBUTUOJKUMYMUPYCYDFUBGHY
      MVOYCYEVAVI $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A a b k $.  $d A k m $.  $d G k $.  $d H k m $.  $d a b f k $.
    $d a f k z $.  $d f k m $.
    sindivcvglem8.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    sindivcvglem8.2 $e |-
      H = { <. a , b >. | ( a e. A /\ b = ( ( sin ` a ) / a ) ) } $.
    sindivcvglem8.3 $e |- A = { z e. RR | z =/= 0 } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem8 $p |- ( ( f : NN --> A /\ f ~~> 0 ) -> ( H o. f ) ~~> 1 ) $=
      ( vk vm cn cv wf cc0 cli wbr wa ccom cfv cr wcel cle c1 cuz wral wrex wi
      cabs clt 1re lt01 1z nnuz eqimss2i nnssz clm0ii mp3an23 adantl w3a
      simpl1l simpl3l sindivcvglem5 wss wne crab ssrab2 eqsstri fss mpan2 fco
      sylancr ffvelrn sylan sindivcvglem6 mpan jca syl11anc c2 cexp co c3 cdiv
      cmin csin neeq1 elrab2 sylib simpr sindivcvglem3 3com23 3expa syl wb
      wfun wceq ffun ax-mp fvco3 mp3an1 sseli opreq1 opreq1d opreq2d oprex
      fvopab4 eqtrd fveq2 id opreq12d breq12d breq1d anbi12d mpbird ex anassrs
      a2d ralimdva reximdva mpd climuzcnv ralbidv2 rexbiia sylibr cz nnz
      sindivcvglem7 reex fopabex2 visset coex cvv rabex eqeltri elisseti
      climsqueeze syl3an1 syl3an2 3expia r19.23adva ) MBCNZOZUUBPQRZSZKNZDUUBTZ
      UAZUBUCZUUFEUUBTZUAZUBUCZSZUUHUUKUDRZUUKUEUDRZSZSZKLNZUFUAZUGZLMUHZUUJUEQ
      RZUUEUURUUFUDRZUUQUIZKMUGZLMUHZUVAUUEUVCUUFUUBUAZUJUAUEUKRZUIZKMUGZLMUHZU
      VFUUDUVKUUCUUDUEUBUCPUEUKRUVKULUMUELKUUBUEMMUNMUEUFUAUOUPUQURUSUTUUEUVJUV
      ELMUUEUURMUCZSZUVIUVDKMUVMUUFMUCZSZUVCUVHUUQUVOUVCUVHUUQUIZUVMUVNUVCUVPUU
      EUVLUVNUVCSZUVPUUEUVLUVQVAZUVHUUQUVRUVHSZUUMUUPUVSUUCUVNUUMUUCUUDUVLUVQUV
      HVBZUVNUVCUUEUVLUVHVCZUUCUVNSZUUIUULUUCMUBUUGOZUVNUUIUUCUBUBDOZMUBUUBOZUW
      CDFGHVDZUUCBUBVEUWEBANZPVFZAUBVGZUBJUWHAUBVHVIZMBUBUUBVJVKZMUBUBDUUBVLVMM
      UBUUFUUGVNVOUUCMUBUUJOZUVNUULBUBEOZUUCUWLABDEFGHIJVPZMBUBEUUBVLVQMUBUUFUU
      JVNVOVRVSUVSUUPUEUVGVTWAWBZWCWDWBZWEWBZUVGWFUAZUVGWDWBZUDRZUWSUEUDRZSZUVS
      UVGUBUCZUVGPVFZSZUVHSUXBUVSUXEUVHUVSUUCUVNUXEUVTUWAUWBUVGBUCZUXEMBUUFUUBV
      NZUWHUXDAUVGUBBUWGUVGPWGJWHWIVSUVRUVHWJVRUXCUXDUVHUXBUXCUVHUXDUXBUVGWKWLW
      MWNUVSUUCUVNUUPUXBWOUVTUWAUWBUUNUWTUUOUXAUWBUUHUWQUUKUWSUDUWBUUHUVGDUAZUW
      QDWPZUUCUVNUUHUXHWQUWDUXIUWFUBUBDWRWSMBUUFDUUBWTXAUWBUXCUXHUWQWQUWBUXFUXC
      UXGBUBUVGUWJXBWNFGUVGUEFNZVTWAWBZWCWDWBZWEWBZUWQUBDUXJUVGWQZUXLUWPUEWEUXN
      UXKUWOWCWDUXJUVGVTWAXCXDXEHUEUWPWEXFXGWNXHUWBUUKUVGEUAZUWSEWPZUUCUVNUUKUX
      OWQUWMUXPUWNBUBEWRWSMBUUFEUUBWTXAUWBUXFUXOUWSWQUXGFGUVGUXJWFUAZUXJWDWBZUW
      SBEUXNUXQUWRUXJUVGWDUXJUVGWFXIUXNXJXKIUWRUVGWDXFXGWNXHZXLUWBUUKUWSUEUDUXS
      XMXNVSXOVRXPWMXQXPXRXSXTYAUUTUVELMUVLUUQUVDKUUSMUUQKLYBYCYDYEUUEUUTUVBLMU
      UEUVLUUTUVBUVLUUEUURYFUCZUUTUVBUURYGUUEUUGUEQRZUXTUUTUVBUUCUWEUUDUYAUWKUU
      BDFGHYHVOUEKUUGUUJUURDUUBFGUBUXMDYIHYJCYKZYLEUUBFGBUXREBUWIYMJUWHAUBYIYNY
      OIYJUYBYLUEYFUNYPYQYRYSYTUUAYA $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d F f $.  $d f x y $.  $d f x z $.
    $( ` ( ( sin `` x ) / x ) ~~> 1 ` as (real) ` x ~~> 0 ` .  (Contributed by
       Paul Chapman, 10-Nov-2011.) $)
    sindivcvg $p |- ( ( F : NN --> ( RR \ { 0 } ) /\ F ~~> 0 ) ->
      ( ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) o. F ) ~~> 1 ) $=
      ( vf vz vy cvv wcel cn cr cc0 csn cdif wf cli wbr wa cv csin cfv cdiv co
      cmpt ccom c1 nnex fex mpan2 adantr wi wceq feq1 breq1 anbi12d coeq2
      breq1d imbi12d c2 cexp c3 cmin copab eqid df-mpt wne crab eldifsn neeq1
      elrab bitr4i eqriv sindivcvglem8 vtoclg mpcom ) BFGZHIJKLZBMZBJNOZPZAVOAQ
      ZRSVSTUAZUBZBUCZUDNOZVPVNVQVPHFGVNUEHVOFBUFUGUHHVOCQZMZWDJNOZPZWAWDUCZUDN
      OZUIVRWCUICBFWDBUJZWGVRWIWCWJWEVPWFVQHVOWDBUKWDBJNULUMWJWHWBUDNWDBWAUNUOU
      PDVOCVSIGZEQUDVSUQURUAUSTUAUTUAUJPAEVAZWAAEWLVBAEVOVTVCAVODQZJVDZDIVEZVSV
      OGWKVSJVDZPVSWOGVSIJVFWNWPDVSIWMVSJVGVHVIVJVKVLVM $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d R n $.  $d n z $.
    circumlem1.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem1.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    $( Lemma for ~ circum . $)
    circumlem1 $p |- ( ( R e. CC /\ z e. NN ) -> ( P ` z ) =
        ( ( ( 2 x. pi ) x. R ) x. ( ( sin ` ( pi / z ) ) / ( pi / z ) ) ) ) $=
      ( cc wcel cv cn wa cfv c2 cmul co cpi cdiv csin wceq opreq2 syl5eq
      opreq1d fveq2d opreq2d opreq12d oprex fvmpt adantl 2cn cr pire recn
      ax-mp mulcli a1i simpl cc0 wne nncn nnne0 divcl mp3an1 syl11anc sincl
      syl pipos gt0ne0ii divne0 mpanl12 syl111anc mulass mul4 syl22anc divcan2
      eqtr3d 3eqtrd mulcl divass syl112anc 2ne0 divcan3 eqtrd eqtr4d ) DHIZAJZK
      IZLZWFCMZNWFOPZDNQOPZWFRPZNRPZSMZOPZOPZWKDOPQWFRPZSMZWQRPZOPZWGWIWPTWEEWF
      NEJZOPZDBNRPZSMZOPZOPWPKCXAWFTZXBWJXEWOOXAWFNOUAXFXDWNDOXFXCWMSXFBWLNRXFB
      WKXARPWLFXAWFWKRUAUBUCUDUEUFGWJWOOUGUHUIWHWTNDOPZWFWQWSOPZOPZOPZWJDXHOPZO
      PZWPWHWTWKDWSOPOPZXGQWSOPZOPZXJWHWKHIZWEWSHIZWTXMTXPWHNQUJQUKIQHIZULQUMUN
      ZUOUPWEWGUQZWHWRHIZWQHIZWQURUSZXQWHYBYAWHWFHIZWFURUSZYBWGYDWEWFUTUIZWGYEW
      EWFVAUIZXRYDYEYBXSQWFVBVCVDZWQVEVFZYHWHYDYEYCYFYGXRQURUSYDYELYCXSQULVGVHQ
      WFVIVJVDZWRWQVBVKZWKDWSVLVKWHNHIZXRWEXQXMXOTYLWHUJUPZXRWHXSUPZXTYKNQDWSVM
      VNWHXNXIXGOWHWFWQOPZWSOPZXNXIWHYOQWSOWHXRYDYEYOQTYNYFYGQWFVOVKUCWHYDYBXQY
      PXITYFYHYKWFWQWSVLVKVPUEVQWHYLWEYDXHHIZXJXLTYMXTYFWHYBXQYQYHYKWQWSVRVDNDW
      FXHVMVNWHXKWOWJOWHXHWNDOWHXHWRWNWHYAYBYCXHWRTYIYHYJWRWQVOVKWHWMWQSWHWMNWQ
      OPZNRPZWQWHWLYRNRWHYLXRYDYEWLYRTYMYNYFYGNQWFVSVTUCWHYBYLNURUSZYSWQTYHYMYT
      WHWAUPWQNWBVKWCUDWDUEUEVQWD $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d G z $.  $d n y z $.  $d x y $.
    circumlem2.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem2.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circumlem2.3 $e |- F = ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) $.
    circumlem2.4 $e |- G = { <. n , y >. | ( n e. NN /\ y = ( pi / n ) ) } $.
    $( Lemma for ~ circum . $)
    circumlem2 $p |- G ~~> 0 $=
      ( vz cpi cc0 cmul co cli wbr cc wcel cn c1 cv cdiv cmpt cz cfv wceq wa
      cuz wral pire recni opreq2 eqid oprex fvmpt rgen cvv nnex mptexg ax-mp
      reccnv 1z elnnuz wf df-mpt wne nncn nnne0 reccl syl11anc fopab ffvelrn
      mpan a1i divrec syl111anc fvopab4 opreq2d 3eqtr4d jca sylbir cr csn cdif
      nnre redivcl recnd pipos gt0ne0ii divne0 syl22anc eldifsn sylanbrc fex
      mp2an 0cn elisseti climmulc2 mp4an mul01 breq2i mpbi ) HNOPQZRSZHORSNTUAZ
      FUBUCFUDZUEQZUFZORSZUCUGUAMUDZXKUHZTUAZXMHUHZNXNPQZUIZUJZMUCUKUHZULXGNUMU
      NZXNUCXMUEQZUIZMUBULXLYCMUBFXMXJYBUBXKXIXMUCUEUOXKUPUCXMUEUQURZUSMXKUBUTU
      AZXKUTUAVAFUBXJUTVBVCZVDVCVEXSMXTXMXTUAXMUBUAZXSXMVFYGXOXRUBTXKVGYGXOFMUB
      TXJXKFMUBXJVHXIUBUAZXITUAZXIOVIZXJTUAXIVJZXIVKZXIVLVMVNUBTXMXKVOVPYGNXMUE
      QZNYBPQZXPXQYGXHXMTUAXMOVIYMYNUIXHYGYAVQXMVJXMVKNXMVRVSFBXMNXIUEQZYMUBHXI
      XMNUEUOLNXMUEUQVTYGXNYBNPYDWAWBWCWDUSONMXKHUCYFUBWEOWFWGZHVGYEHUTUAFBUBYP
      YOHLYHYOWEUAZYOOVIZYOYPUAYHNWEUAZXIWEUAYJYQYSYHUMVQZXIWHYLNXIWIVSYHXHNOVI
      ZYIYJYRYHNYTWJUUAYHNUMWKWLVQYKYLNXIWMWNYOWEOWOWPVNVAUBYPUTHWQWROTWSWTXAXB
      XFOHRXHXFOUIYANXCVCXDXE $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A y $.  $d F z $.  $d G z $.  $d P z $.  $d R n y z $.  $d x y z $.
    circumlem3.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem3.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circumlem3.3 $e |- F = ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) $.
    circumlem3.4 $e |- G = { <. n , y >. | ( n e. NN /\ y = ( pi / n ) ) } $.
    circum10.5 $e |- R e. RR $.
    $( Lemma for ~ circum . $)
    circumlem3 $p |- P ~~> ( ( 2 x. pi ) x. R ) $=
      ( vz c2 cpi cmul co c1 cli ccom 2cn pire recni mulcli cz 1z elisseti cr
      cc0 csn cdif cv csin cfv cdiv cmpt coeq1i cn wf wbr wcel wceq wa copab
      eleq1 opreq2 eqeq2d anbi12d cbvopab1v eqtri wne a1i nnre nnne0 redivcl
      syl111anc cc recnd pipos gt0ne0ii nncn divne0 syl22anc eldifsn sylanbrc
      fopab circumlem2 sindivcvg mp2an eqbrtri oprex df-mpt fnopab2 wfun
      resincl syl3an1 3anidm12 sylbi ffun ax-mp fvco3 mp3an12 ffvelrn mpan
      sylancr eqeltrd circumlem1 fvopab4 fveq2d fveq2 id opreq12d syl 3eqtrd
      opreq2d eqtr4d jca climmulci mulid1i breqtri ) DOPQRZEQRZSQRYCTYCSNGHUAZD
      YBEOPUBPUCUDUEEMUDZUEZSUFUGUHYDAUIUJUKULZAUMZUNUOZYHUPRZUQZHUAZSTGYKHKURU
      SYGHUTZHUJTVAYLSTVANBUSYGPNUMZUPRZHHFUMZUSVBZBUMZPYPUPRZVCZVDZFBVEYNUSVBZ
      YRYOVCZVDZNBVELUUAUUDFBNYPYNVCZYQUUBYTUUCYPYNUSVFUUEYSYOYRYPYNPUPVGZVHVIV
      JVKUUBYOUIVBZYOUJVLZYOYGVBZUUBPUIVBZYNUIVBYNUJVLZUUGUUJUUBUCVMZYNVNYNVOZP
      YNVPVQUUBPVRVBPUJVLZYNVRVBUUKUUHUUBPUULVSUUNUUBPUCVTWAVMYNWBUUMPYNWCWDYOU
      IUJWEWFZWGZABCDEFGHIJKLWHAHWIWJWKFBUSOYPQRZECOUPRUNUOQRZQRZDUUQUURQWLDFUS
      UUSUQYQYRUUSVCVDFBVEJFBUSUUSWMVKWNUUBYNYDUOZVRVBYNDUOZYCUUTQRZVCUUBUUTYNH
      UOZGUOZVRGWOZYMUUBUUTUVDVCYGUIGUTZUVEABYGUIYJGGYKYHYGVBZYRYJVCVDABVEKABYG
      YJWMVKZUVGYHUIVBZYHUJVLZVDYJUIVBZYHUIUJWEUVIUVJUVKUVIYIUIVBUVIUVJUVKYHWPY
      IYHVPWQWRWSWGZYGUIGWTXAUUPUSYGYNGHXBXCZUUBUVDUUBUVFUVCYGVBZUVDUIVBUVLYMUU
      BUVNUUPUSYGYNHXDXEYGUIUVCGXDXFVSXGUUBUVAYCYOUNUOZYOUPRZQRZUVBEVRVBUUBUVAU
      VQVCYENCDEFIJXHXEUUBUUTUVPYCQUUBUUTUVDYOGUOZUVPUVMUUBUVCYOGFBYNYSYOUSHUUF
      LPYNUPWLXIXJUUBUUIUVRUVPVCUUOABYOYJUVPYGGYHYOVCZYIUVOYHYOUPYHYOUNXKUVSXLX
      MUVHUVOYOUPWLXIXNXOXPXQXRXSYCYFXTYA $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A y $.  $d R n y $.
    circum.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circum.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circum.3 $e |- R e. RR $.
    $( The circumference of a circle of radius ` R ` , defined as the limit as
       ` n ~~> +oo ` of the perimeter of an inscribed n-sided isogons, is
       ` ( ( 2 x. pi ) x. R ) ` .  (Contributed by Paul Chapman,
       10-Nov-2011.) $)
    circum $p |- P ~~> ( ( 2 x. pi ) x. R ) $=
      ( vy cr cc0 csn cdif cv csin cfv cdiv co cmpt cn wcel cpi wceq wa copab
      eqid circumlem3 ) DHABCDDIJKLDMZNOUGPQRZUGSTHMUAUGPQUBUCDHUDZEFUHUEUIUEGU
      F $.
      $( [10-Nov-2012] $)
  $}

