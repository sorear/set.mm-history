$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Real and complex numbers (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Quotient of two negatives.  (Moved into main set.mm as ~ div2neg and may
     be deleted by mathbox owner, PC. --NM 20-Sep-2013.) $)
  div2negOLD $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
                  ( -u A / -u B ) = ( A / B ) ) $=
    ( div2neg ) ABC $.
    $( [10-Nov-2012] $)

  ${
    $d k m $.
    $( Utility lemma to convert between ` m <_ k ` and ` k e. ( ZZ>= `` m ) `
       in limit theorems.  (Contributed by Paul Chapman, 10-Nov-2012.) $)
    climuzcnv $p |- ( m e. NN -> ( ( k e. ( ZZ>= ` m ) -> ph ) <->
                                   ( k e. NN -> ( m <_ k -> ph ) ) ) ) $=
      ( cv cn wcel cuz cfv wi cle wbr wa elnnuz uztrn sylan2b sylibr expcom nnz
      c1 cz eluzle a1i jcad biimpri syl3an1 syl3an2 3expib impbid imbi1d impexp
      w3a eluz2 syl6bb ) CDZEFZBDZUNGHFZAIUPEFZUNUPJKZLZAIURUSAIIUOUQUTAUOUQUTU
      OUQURUSUQUOURUQUOLUPSGHZFZURUOUQUNVAFVBUNMUNUPSNOUPMPQUQUSIUOUNUPUAUBUCUO
      URUSUQURUOUPTFZUSUQUPRUOUNTFZVCUSUQUNRUQVDVCUSUKUNUPULUDUEUFUGUHUIURUSAUJ
      UM $.
      $( [10-Nov-2012] $)
  $}

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem1 $p |- ( A e. ( 0 (,] 1 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( cc0 c1 co wcel c3 cexp cdiv cmin clt wbr wa c2 cle cr 3nn syl11anc syl cc
    wceq cioc csin cfv sin01bnd crp wi w3a 0re 1re elioc2 mp2an simp1bi simp2bi
    wb elrp sylanbrc rpre cn0 nnnn0 ax-mp reexpcl mpan2 wne 3re redivcl mp3an23
    nnne0i 3syl resubcl resincl ltle rpgt0 lediv1 syl112anc rpcn recn divsubdir
    cn rpne0 divid nncn pm3.2i divdiv23 mp3an3 syl12anc exp1 opreq2d caddc df-3
    eqcomi ax-1cn 2cn subadd2i mpbir opreq2i 1nn0 nnge1 expsub syl5reqr opreq1d
    eqtr3d opreq12d eqtrd breq1d bitrd sylibd breq2d anim12d mpd ) ABCUADEZAAFG
    DZFHDZIDZAUBUCZJKZXNAJKZLZCAMGDZFHDZIDZXNAHDZNKZYACNKZLZAUDXJAUEEZXQYDUFXJA
    OEZBAJKZYEXJYFYGACNKZBOECOEXJYFYGYHUGUNUHUIBCAUJUKZULXJYFYGYHYIUMAUOUPYEXOY
    BXPYCYEXOXMXNNKZYBYEXMOEZXNOEZXOYJUFYEYFXLOEZYKAUQZYEYFXKOEZYMYNYFFUREZYOFV
    REZYPPFUSUTZAFVAVBZYOFOEFBVCZYMVDFPVGZXKFVEVFVHZAXLVIQZYEYFYLYNAVJRZXMXNVKQ
    YEYJXMAHDZYANKZYBYEYKYLYFYGYJUUFUNUUCUUDYNAVLZXMXNAVMVNYEUUEXTYANYEUUEAAHDZ
    XLAHDZIDZXTYEASEZXLSEZUUKABVCZUUEUUJTAVOZYEYMUULUUBXLVPRUUNAVSZAXLAVQVNYEUU
    HCUUIXSIYEUUKUUMUUHCTUUNUUOAVTQZYEXKAHDZFHDZUUIXSYEXKSEZUUKUUMUURUUITZYEYFY
    OUUSYNYSXKVPVHUUNUUOUUSUUKUUMLZFSEZYTLUUTUVBYTYQUVBPFWAUTZUUAWBXKAFWCWDWEYE
    UUQXRFHYEXKACGDZHDZUUQXRYEUVDAXKHYEUUKUVDATUUNAWFRWGYEXRAFCIDZGDZUVEUVFMAGU
    VFMTMCWHDZFTFUVHWIWJFCMUVCWKWLWMWNWOYEUUKUUMUVGUVETZUUNUUOUVAYPCUREZUVIYRWP
    UVAYPUVJUGCFNKZUVIYQUVKPFWQUTAFCWRVBVFQWSXAWTXAXBXCXDXEXFYEXPXNANKZYCYEYLYF
    XPUVLUFUUDYNXNAVKQYEUVLYAUUHNKZYCYEYLYFYFYGUVLUVMUNUUDYNYNUUGXNAAVMVNYEUUHC
    YANUUPXGXEXFXHRXI $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem2 $p |- ( A e. ( -u 1 [,) 0 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( c1 cneg cc0 co wcel c2 cexp c3 cdiv cmin csin cle wbr wa cr wb 1re adantr
    wceq cico cfv clt w3a cioc renegcli 0re elico2 mp2an renegcl lt0neg1 biimpa
    3ad2ant1 3adant2 lenegcon1 mpan2 biimpar 3adant3 sylbi elioc2 sindivcvglem1
    3jca biimpri 3syl cc wne simp1bi recn simp3bi gt0ne0 sylan syldan necon3bid
    negeq0 mpbird syl11anc sqneg opreq1d opreq2d sincl div2neg syl3an1 3anidm12
    syl sinneg eqtrd breq12d breq1d anbi12d mpbid ) ABCZDUAEFZBACZGHEZIJEZKEZWM
    LUBZWMJEZMNZWRBMNZOZBAGHEZIJEZKEZALUBZAJEZMNZXFBMNZOZWLWMPFZDWMUCNZWMBMNZUD
    ZWMDBUEEFZXAWLAPFZWKAMNZADUCNZUDZXMWKPFDPFZWLXRQBRUFUGWKDAUHUIZXRXJXKXLXOXP
    XJXQAUJZUMXOXQXKXPXOXQXKAUKULZUNXOXPXLXQXOXLXPXOBPFZXLXPQRABUOUPUQURVBUSXNX
    MXSYCXNXMQUGRDBWMUTUIVCWMVAVDWLAVEFZADVFZXAXIQWLXOYDWLXOXPXQXTVGZAVHZWDWLXO
    XQYEYFWLXOXPXQXTVIXOXQOYEWMDVFZXOXQXKYHYBXOXJXKYHYAWMVJVKVLXOYEYHQZXQXOYDYI
    YGYDADWMDAVNVMWDSVOVPYDYEOZWSXGWTXHYJWPXDWRXFMYDWPXDTYEYDWOXCBKYDWNXBIJAVQV
    RVSSYJWRXECZWMJEZXFYDWRYLTYEYDWQYKWMJAWEVRSYDYEYLXFTZYDXEVEFYDYEYMAVTXEAWAW
    BWCWFZWGYJWRXFBMYNWHWIVPWJ $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem3 $p |- ( ( A e. RR /\ ( abs ` A ) < 1 /\ A =/= 0 ) ->
                        ( ( 1 - ( ( A ^ 2 ) / 3 ) ) <_ ( ( sin ` A ) / A ) /\
                          ( ( sin ` A ) / A ) <_ 1 ) ) $=
    ( cr wcel c1 clt wbr cc0 w3a cioo co cle wa wb 1re ax-mp elioo2 mp2an sylbi
    cun 0re cabs cfv wne cneg csn cdif c2 cexp c3 cdiv cmin abslt mpan2 pm5.32i
    csin cxr renegcli biimpri 3expb anim1i 3impa eldifsn sylibr wo difid uneq2i
    rexr c0 cin wceq wn ltnr simp3bi simp2bi jaoi mto mtbir disjsn mpbir undif4
    elun 3eqtr3i un23 lt01 lt0neg2 biimpa ioojoin mpanr12 eqtr3i difeq1i eleq2i
    un0 mp3an bitr3i cico simp1 wi ltle mpan 3adant3 simp3 elico2 sindivcvglem2
    imp 3jca syl cioc simp2 3adant2 elioc2 sindivcvglem1 ) ABCZAUAUBDEFZAGUCZHZ
    ADUDZDIJZGUEZUFZCZDAUGUHJUIUJJUKJAUOUBAUJJZKFYADKFLZXOAXQCZXNLZXTXLXMXNYDXL
    XMLZYCXNYEXLXPAEFZADEFZLZLYCXLXMYHXLDBCZXMYHMNADULUMUNXLYFYGYCYCXLYFYGHZXPU
    PCZDUPCZYCYJMXPBCZYKDNUQZXPVGOZYIYLNDVGOZXPDAPQURUSRUTVAAXQGVBVCXTAXPGIJZCZ
    AGDIJZCZVDZYBXTAYQYSSZCUUAUUBXSAUUBXRSZXRUFZUUBXSUUBXRXRUFZSZUUBVHSUUDUUBUU
    EVHUUBXRVEVFUUBXRVIVHVJZUUFUUDVJUUGGUUBCZVKUUHGYQCZGYSCZVDZUUKGGEFZGBCZUULV
    KTGVLOUUIUULUUJUUIUUMXPGEFZUULYKGUPCZUUIUUMUUNUULHMYOUUMUUOTGVGOZXPGGPQVMUU
    JUUMUULGDEFZUUOYLUUJUUMUULUUQHMUUPYPGDGPQVNVOVPGYQYSWAVQUUBGVRVSUUBXRXRVTOU
    UBWLWBUUCXQXRYQXRSYSSZUUCXQYQXRYSWCYMUUMYIUURXQVJZYNTNYMUUMYIHUUNUUQUUSYIUU
    QUUNNWDYIUUQUUNDWEWFQWDXPGDWGWHWMWIWJWIWKAYQYSWAWNYRYBYTYRAXPGWOJCZYBYRXLXP
    AKFZAGEFZHZUUTYRXLYFUVBHZUVCYKUUOYRUVDMYOUUPXPGAPQUVDXLUVAUVBXLYFUVBWPXLYFU
    VAUVBXLYFUVAYMXLYFUVAWQYNXPAWRWSXDWTXLYFUVBXAXERYMUUMUUTUVCMYNTXPGAXBQVCAXC
    XFYTAGDXGJCZYBYTXLGAEFZADKFZHZUVEYTXLUVFYGHZUVHUUOYLYTUVIMUUPYPGDAPQUVIXLUV
    FUVGXLUVFYGWPXLUVFYGXHXLYGUVGUVFXLYGUVGXLYIYGUVGWQNADWRUMXDXIXERUUMYIUVEUVH
    MTNGDAXJQVCAXKXFVORXF $.
    $( [10-Nov-2012] $)

  $( Lemma for ~ sindivcvg . $)
  sindivcvglem4 $p |- ( ( A e. CC /\ ( B e. RR /\ 0 < B ) ) ->
      ( ( abs ` ( ( 1 - ( ( A ^ 2 ) / 3 ) ) - 1 ) ) < B <->
        ( abs ` A ) < ( sqr ` ( 3 x. B ) ) ) ) $=
    ( cr wcel cc0 clt wbr wa cc cle c1 c2 co c3 cdiv cmin cabs cfv wceq 3re 0re
    cexp cmul csqr wb wi ltle mpan imdistani cneg caddc sqcl nncni nnne0i divcl
    wne mp3an23 ax-1cn negsub opreq1d negcl pncan2 sylancr eqtr3d fveq2d absneg
    3nn 3syl eqtrd breq1d adantr abscl absge0 jca remulcl nngt0i ltleii mpanl12
    mulge0 resqrcl syl11anc sqrge0 lt2sq syl2an cn0 2nn0 absexp mpan2 breqan12d
    eqcomd sqsqr bitrd syl pm3.2i ltdiv1 mp3an3 absdiv absid mp2an opreq2i recn
    syl6eq divcan3 3bitrd bitr4d sylan2 ) BCDZEBFGZHAIDZXGEBJGZHZKALUBMZNOMZPMZ
    KPMZQRZBFGZAQRZNBUCMZUDRZFGZUEXGXHXJECDXGXHXJUFUAEBUGUHUIXIXKHZXQXMQRZBFGZY
    AXIXQYDUEXKXIXPYCBFXIXPXMUJZQRZYCXIXOYEQXIKYEUKMZKPMZXOYEXIYGXNKPXIXLIDZXMI
    DZYGXNSZAULZYINIDZNEUPZYJNVGUMZNVGUNZXLNUOUQZKIDZYJYKURKXMUSUHVHUTXIYRYEIDZ
    YHYESURXIYIYJYSYLYQXMVAVHKYEVBVCVDVEXIYIYJYFYCSYLYQXMVFVHVIVJVKYBYAXLQRZXSF
    GZYTNOMZXSNOMZFGZYDYBYAXRLUBMZXTLUBMZFGZUUAXIXRCDZEXRJGZHXTCDZEXTJGZHYAUUGU
    EXKXIUUHUUIAVLAVMVNXKUUJUUKXKXSCDZEXSJGZUUJXGUULXJNCDZXGUULTNBVOUHVKZUUNENJ
    GZXKUUMTENUATNVGVPZVQZNBVSVRZXSVTWAXKUULUUMUUKUUOUUSXSWBWAVNXRXTWCWDXIXKUUE
    YTUUFXSFXIYTUUEXILWEDYTUUESWFALWGWHWJXKUULUUMUUFXSSUUOUUSXSWKWAWIWLXIYTCDZU
    ULUUAUUDUEZXKXIYIUUTYLXLVLWMUUOUUTUULUUNENFGZHUVAUUNUVBTUUQWNYTXSNWOWPWDXIX
    KUUBYCUUCBFXIYCUUBXIYCYTNQRZOMZUUBXIYIYCUVDSZYLYIYMYNUVEYOYPXLNWQUQWMUVCNYT
    OUUNUUPUVCNSTUURNWRWSWTXBWJXKBIDZUUCBSZXGUVFXJBXAVKUVFYMYNUVGYOYPBNXCUQWMWI
    XDXEXF $.
    $( [10-Nov-2012] $)

  ${
    $d a b $.
    sindivcvglem5.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem5 $p |- G : RR --> RR $=
      ( cr c1 cv c2 cexp co c3 cdiv cmin wcel resqcl cc0 wne 3re 3nn nnne0i 1re
      redivcl mp3an23 resubcl mpan 3syl fopab ) BCEEFBGZHIJZKLJZMJZADUHENUIENZU
      JENZUKENZUHOULKENKPQUMRKSTUIKUBUCFENUMUNUAFUJUDUEUFUG $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A a b $.  $d a z $.
    sindivcvglem6.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    sindivcvglem6.2 $e |-
      H = { <. a , b >. | ( a e. A /\ b = ( ( sin ` a ) / a ) ) } $.
    sindivcvglem6.3 $e |- A = { z e. RR | z =/= 0 } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem6 $p |- H : A --> RR $=
      ( cr cv csin cfv cdiv co wcel cc0 wne wa neeq1 elrab2 resincl sylbi fopab
      redivcl syl3an1 3anidm12 ) EFBJEKZLMZUHNOZDHUHBPUHJPZUHQRZSUJJPZAKZQRULAU
      HJBUNUHQTIUAUKULUMUKUIJPUKULUMUHUBUIUHUEUFUGUCUD $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d F a b k $.  $d F j k x y $.  $d G j k y $.
    sindivcvglem7.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem7 $p |- ( ( F : NN --> RR /\ F ~~> 0 ) -> ( G o. F ) ~~> 1 ) $=
      ( vj vk cn cr cc0 wbr wa clt cfv c1 cmin co wi wcel c3 vy vx cli cle ccom
      wf cv cabs wral wrex cmul csqr wss ax-resscn fss mpan2 3re remulcl adantr
      cc mpan 3nn nngt0i mulgt0 mpanl12 0re sylancr mpd resqrcl syl11anc sqrgt0
      ltle wb 0cn climfnn biimpa wceq breq2 imbi2d ralbidv rexbidv imbi12d syl5
      rcla4v com23 imp impcom sylanl1 c2 cexp cdiv ffvelrn subid1 fveq2d breq1d
      recn syl sindivcvglem4 bitr4d sylan wfun sindivcvglem5 ax-mp fvco3 mp3an1
      ffun opreq1 opreq1d opreq2d oprex fvopab4 expcom adantlr pm5.74d ralbidv2
      eqtrd exp3a mpbid anassrs ex ralrimiva fco sylancl ax-1cn biimprd ) HIAUF
      ZAJUCKZLZJUAUGZMKZFUGGUGZUDKZYKBAUEZNZOPQZUHNZYIMKZRZGHUIZFHUJZRZUAIUIZYM
      OUCKZYHUUAUAIYHYIISZLYJYTYHUUDYJYTYHUUDYJLZLZYLYKANZJPQZUHNZTYIUKQZULNZMK
      ZRZGHUIZFHUJZYTYFHUTAUFZYGUUEUUOYFIUTUMZUUPUNHIUTAUOUPUUEUUPYGLZUUOUUEUUK
      ISZJUUKMKZUURUUORZUUEUUJISZJUUJUDKZUUSUUDUVBYJTISZUUDUVBUQTYIURVAUSZUUEJU
      UJMKZUVCUVDJTMKUUEUVFUQTVBVCTYIVDVEZUUEJISUVBUVFUVCRVFUVEJUUJVLVGVHUUJVIV
      JUUEUVBUVFUUTUVEUVGUUJVKVJUUSUUTUVAUUSUURUUTUUOUURJUBUGZMKZYLUUIUVHMKZRZG
      HUIZFHUJZRZUBIUIZUUSUUTUUORZUUPYGUVOUUPJUTSYGUVOVMVNUBJFGAVOUPVPUVNUVPUBU
      UKIUVHUUKVQZUVIUUTUVMUUOUVHUUKJMVRUVQUVLUUNFHUVQUVKUUMGHUVQUVJUULYLUVHUUK
      UUIMVRVSVTWAWBWDWCWEWFVJWGWHUUFUUNYSFHUUFUUMYRGHHUUFYKHSZUUMYRYFUUEUVRUUM
      YRVMZRZYGUUEYFUVTUUEYFUVRUVSYFUVRLZUUEUVSUWAUUELZUULYQYLUWBUULOUUGWIWJQZT
      WKQZPQZOPQZUHNZYIMKZYQUWAUUGUTSZUUEUULUWHVMUWAUUGISZUWIHIYKAWLZUUGWPWQUWI
      UUELUULUUGUHNZUUKMKZUWHUWIUULUWMVMUUEUWIUUIUWLUUKMUWIUUHUUGUHUUGWMWNWOUSU
      UGYIWRWSWTUWAYQUWHVMUUEUWAYPUWGYIMUWAYOUWFUHUWAYNUWEOPUWAYNUUGBNZUWEBXAZY
      FUVRYNUWNVQIIBUFZUWOBCDEXBZIIBXFXCHIYKBAXDXEUWAUWJUWNUWEVQUWKCDUUGOCUGZWI
      WJQZTWKQZPQUWEIBUWRUUGVQZUWTUWDOPUXAUWSUWCTWKUWRUUGWIWJXGXHXIEOUWDPXJXKWQ
      XPXHWNWOUSWSVSXLXQWGXMXNXOWAXRXSXTYAYFUUBUUCRYGYFUUCUUBYFHUTYMUFZOUTSUUCU
      UBVMYFHIYMUFZUUQUXBUWPYFUXCUWQHIIBAYBVAUNHIUTYMUOYCYDUAOFGYMVOYCYEUSVH $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A a b k $.  $d A k m $.  $d G k $.  $d H k m $.  $d a b f k $.
    $d a f k z $.  $d f k m $.
    sindivcvglem8.1 $e |-
      G = { <. a , b >. | ( a e. RR /\ b = ( 1 - ( ( a ^ 2 ) / 3 ) ) ) } $.
    sindivcvglem8.2 $e |-
      H = { <. a , b >. | ( a e. A /\ b = ( ( sin ` a ) / a ) ) } $.
    sindivcvglem8.3 $e |- A = { z e. RR | z =/= 0 } $.
    $( Lemma for ~ sindivcvg . $)
    sindivcvglem8 $p |- ( ( f : NN --> A /\ f ~~> 0 ) -> ( H o. f ) ~~> 1 ) $=
      ( vk vm cn wbr wa cfv cr wcel c1 co cv wf cc0 cli ccom cle wral wrex cabs
      cuz wi clt 1re lt01 nnuz eqimss2i nnssz clm0ii mp3an23 adantl w3a simpl1l
      1z simpl3l sindivcvglem5 wss wne ssrab2 eqsstri fss mpan2 sylancr ffvelrn
      crab fco sylan sindivcvglem6 mpan jca syl11anc c2 cexp c3 cdiv cmin neeq1
      csin elrab2 sylib simpr sindivcvglem3 3com23 3expa syl wb wfun wceq ax-mp
      ffun fvco3 mp3an1 sseli opreq1 opreq1d opreq2d oprex eqtrd fveq2 opreq12d
      fvopab4 id breq12d breq1d anbi12d mpbird ex anassrs a2d ralimdva reximdva
      mpd climuzcnv ralbidv2 rexbiia sylibr cz sindivcvglem7 reex fopabex2 coex
      nnz visset cvv rabex eqeltri elisseti climsqueeze syl3an1 syl3an2 3expia
      rexlimdva ) MBCUAZUBZUUBUCUDNZOZKUAZDUUBUEZPZQRZUUFEUUBUEZPZQRZOZUUHUUKUF
      NZUUKSUFNZOZOZKLUAZUJPZUGZLMUHZUUJSUDNZUUEUURUUFUFNZUUQUKZKMUGZLMUHZUVAUU
      EUVCUUFUUBPZUIPSULNZUKZKMUGZLMUHZUVFUUDUVKUUCUUDSQRUCSULNUVKUMUNSLKUUBSMM
      VCMSUJPUOUPUQURUSUTUUEUVJUVELMUUEUURMRZOZUVIUVDKMUVMUUFMRZOZUVCUVHUUQUVOU
      VCUVHUUQUKZUVMUVNUVCUVPUUEUVLUVNUVCOZUVPUUEUVLUVQVAZUVHUUQUVRUVHOZUUMUUPU
      VSUUCUVNUUMUUCUUDUVLUVQUVHVBZUVNUVCUUEUVLUVHVDZUUCUVNOZUUIUULUUCMQUUGUBZU
      VNUUIUUCQQDUBZMQUUBUBZUWCDFGHVEZUUCBQVFUWEBAUAZUCVGZAQVNZQJUWHAQVHVIZMBQU
      UBVJVKZMQQDUUBVOVLMQUUFUUGVMVPUUCMQUUJUBZUVNUULBQEUBZUUCUWLABDEFGHIJVQZMB
      QEUUBVOVRMQUUFUUJVMVPVSVTUVSUUPSUVGWAWBTZWCWDTZWETZUVGWGPZUVGWDTZUFNZUWSS
      UFNZOZUVSUVGQRZUVGUCVGZOZUVHOUXBUVSUXEUVHUVSUUCUVNUXEUVTUWAUWBUVGBRZUXEMB
      UUFUUBVMZUWHUXDAUVGQBUWGUVGUCWFJWHWIVTUVRUVHWJVSUXCUXDUVHUXBUXCUVHUXDUXBU
      VGWKWLWMWNUVSUUCUVNUUPUXBWOUVTUWAUWBUUNUWTUUOUXAUWBUUHUWQUUKUWSUFUWBUUHUV
      GDPZUWQDWPZUUCUVNUUHUXHWQUWDUXIUWFQQDWSWRMBUUFDUUBWTXAUWBUXCUXHUWQWQUWBUX
      FUXCUXGBQUVGUWJXBWNFGUVGSFUAZWAWBTZWCWDTZWETZUWQQDUXJUVGWQZUXLUWPSWEUXNUX
      KUWOWCWDUXJUVGWAWBXCXDXEHSUWPWEXFXJWNXGUWBUUKUVGEPZUWSEWPZUUCUVNUUKUXOWQU
      WMUXPUWNBQEWSWRMBUUFEUUBWTXAUWBUXFUXOUWSWQUXGFGUVGUXJWGPZUXJWDTZUWSBEUXNU
      XQUWRUXJUVGWDUXJUVGWGXHUXNXKXIIUWRUVGWDXFXJWNXGZXLUWBUUKUWSSUFUXSXMXNVTXO
      VSXPWMXQXPXRXSXTYAUUTUVELMUVLUUQUVDKUUSMUUQKLYBYCYDYEUUEUUTUVBLMUUEUVLUUT
      UVBUVLUUEUURYFRZUUTUVBUURYKUUEUUGSUDNZUXTUUTUVBUUCUWEUUDUYAUWKUUBDFGHYGVP
      SKUUGUUJUURDUUBFGQUXMDYHHYICYLZYJEUUBFGBUXREBUWIYMJUWHAQYHYNYOIYIUYBYJSYF
      VCYPYQYRYSYTUUAYA $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d F f $.  $d f x y $.  $d f x z $.
    $( ` ( ( sin `` x ) / x ) ~~> 1 ` as (real) ` x ~~> 0 ` .  (Contributed by
       Paul Chapman, 10-Nov-2011.) $)
    sindivcvg $p |- ( ( F : NN --> ( RR \ { 0 } ) /\ F ~~> 0 ) ->
      ( ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) o. F ) ~~> 1 ) $=
      ( vf vz vy cvv wcel cn cr cc0 wf cli wbr wa cv cdiv co ccom c1 wi csn cfv
      cdif csin cmpt nnex fex mpan2 adantr wceq feq1 breq1 anbi12d coeq2 breq1d
      imbi12d c2 cexp c3 cmin copab eqid df-mpt crab eldifsn neeq1 elrab bitr4i
      wne eqriv sindivcvglem8 vtoclg mpcom ) BFGZHIJUAUCZBKZBJLMZNZAVOAOZUDUBVS
      PQZUEZBRZSLMZVPVNVQVPHFGVNUFHVOFBUGUHUIHVOCOZKZWDJLMZNZWAWDRZSLMZTVRWCTCB
      FWDBUJZWGVRWIWCWJWEVPWFVQHVOWDBUKWDBJLULUMWJWHWBSLWDBWAUNUOUPDVOCVSIGZEOS
      VSUQURQUSPQUTQUJNAEVAZWAAEWLVBAEVOVTVCAVODOZJVIZDIVDZVSVOGWKVSJVIZNVSWOGV
      SIJVEWNWPDVSIWMVSJVFVGVHVJVKVLVM $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d R n $.  $d n z $.
    circumlem1.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem1.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    $( Lemma for ~ circum . $)
    circumlem1 $p |- ( ( R e. CC /\ z e. NN ) -> ( P ` z ) =
        ( ( ( 2 x. pi ) x. R ) x. ( ( sin ` ( pi / z ) ) / ( pi / z ) ) ) ) $=
      ( cc wcel cfv c2 cmul co cpi cdiv csin wceq opreq2d a1i syl111anc opreq1d
      cv cn wa opreq2 syl5eq fveq2d opreq12d oprex fvmpt adantl pire recn ax-mp
      2cn mulcli simpl cc0 wne nncn nnne0 divcl mp3an1 syl11anc sincl syl pipos
      gt0ne0ii divne0 mpanl12 mulass mul4 syl22anc divcan2 eqtr3d 3eqtrd divass
      cr mulcl syl112anc 2ne0 divcan3 eqtrd eqtr4d ) DHIZAUBZUCIZUDZWFCJZKWFLMZ
      DKNLMZWFOMZKOMZPJZLMZLMZWKDLMNWFOMZPJZWQOMZLMZWGWIWPQWEEWFKEUBZLMZDBKOMZP
      JZLMZLMWPUCCXAWFQZXBWJXEWOLXAWFKLUEXFXDWNDLXFXCWMPXFBWLKOXFBWKXAOMWLFXAWF
      WKOUEUFUAUGRUHGWJWOLUIUJUKWHWTKDLMZWFWQWSLMZLMZLMZWJDXHLMZLMZWPWHWTWKDWSL
      MLMZXGNWSLMZLMZXJWHWKHIZWEWSHIZWTXMQXPWHKNUONVRINHIZULNUMUNZUPSWEWGUQZWHW
      RHIZWQHIZWQURUSZXQWHYBYAWHWFHIZWFURUSZYBWGYDWEWFUTUKZWGYEWEWFVAUKZXRYDYEY
      BXSNWFVBVCVDZWQVEVFZYHWHYDYEYCYFYGXRNURUSYDYEUDYCXSNULVGVHNWFVIVJVDZWRWQV
      BTZWKDWSVKTWHKHIZXRWEXQXMXOQYLWHUOSZXRWHXSSZXTYKKNDWSVLVMWHXNXIXGLWHWFWQL
      MZWSLMZXNXIWHYONWSLWHXRYDYEYONQYNYFYGNWFVNTUAWHYDYBXQYPXIQYFYHYKWFWQWSVKT
      VORVPWHYLWEYDXHHIZXJXLQYMXTYFWHYBXQYQYHYKWQWSVSVDKDWFXHVLVMWHXKWOWJLWHXHW
      NDLWHXHWRWNWHYAYBYCXHWRQYIYHYJWRWQVNTWHWMWQPWHWMKWQLMZKOMZWQWHWLYRKOWHYLX
      RYDYEWLYRQYMYNYFYGKNWFVQVTUAWHYBYLKURUSZYSWQQYHYMYTWHWASWQKWBTWCUGWDRRVPW
      D $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d G z $.  $d n y z $.  $d x y $.
    circumlem2.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem2.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circumlem2.3 $e |- F = ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) $.
    circumlem2.4 $e |- G = { <. n , y >. | ( n e. NN /\ y = ( pi / n ) ) } $.
    $( Lemma for ~ circum . $)
    circumlem2 $p |- G ~~> 0 $=
      ( vz cpi cc0 co cc wcel cn cdiv cmul cli wbr c1 cv cmpt cfv wceq cuz wral
      cz wa pire recni opreq2 eqid oprex fvmpt rgen nnex mptexg ax-mp reccnv 1z
      cvv elnnuz wf df-mpt wne nncn nnne0 reccl syl11anc fopab ffvelrn mpan a1i
      divrec syl111anc fvopab4 opreq2d 3eqtr4d jca sylbir csn cdif nnre redivcl
      cr recnd pipos gt0ne0ii divne0 syl22anc eldifsn sylanbrc fex 0cn elisseti
      mp2an climmulc2 mp4an mul01 breq2i mpbi ) HNOUAPZUBUCZHOUBUCNQRZFSUDFUEZT
      PZUFZOUBUCZUDUKRMUEZXKUGZQRZXMHUGZNXNUAPZUHZULZMUDUIUGZUJXGNUMUNZXNUDXMTP
      ZUHZMSUJXLYCMSFXMXJYBSXKXIXMUDTUOXKUPUDXMTUQURZUSMXKSVERZXKVERUTFSXJVEVAV
      BZVCVBVDXSMXTXMXTRXMSRZXSXMVFYGXOXRSQXKVGYGXOFMSQXJXKFMSXJVHXISRZXIQRZXIO
      VIZXJQRXIVJZXIVKZXIVLVMVNSQXMXKVOVPYGNXMTPZNYBUAPZXPXQYGXHXMQRXMOVIYMYNUH
      XHYGYAVQXMVJXMVKNXMVRVSFBXMNXITPZYMSHXIXMNTUOLNXMTUQVTYGXNYBNUAYDWAWBWCWD
      USONMXKHUDYFSWIOWEWFZHVGYEHVERFBSYPYOHLYHYOWIRZYOOVIZYOYPRYHNWIRZXIWIRYJY
      QYSYHUMVQZXIWGYLNXIWHVSYHXHNOVIZYIYJYRYHNYTWJUUAYHNUMWKWLVQYKYLNXIWMWNYOW
      IOWOWPVNUTSYPVEHWQWTOQWRWSXAXBXFOHUBXHXFOUHYANXCVBXDXE $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A y $.  $d F z $.  $d G z $.  $d P z $.  $d R n y z $.  $d x y z $.
    circumlem3.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circumlem3.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circumlem3.3 $e |- F = ( x e. ( RR \ { 0 } ) |-> ( ( sin ` x ) / x ) ) $.
    circumlem3.4 $e |- G = { <. n , y >. | ( n e. NN /\ y = ( pi / n ) ) } $.
    circum10.5 $e |- R e. RR $.
    $( Lemma for ~ circum . $)
    circumlem3 $p |- P ~~> ( ( 2 x. pi ) x. R ) $=
      ( cpi cmul co cr cn wcel wceq vz c2 c1 cli ccom 2cn recni mulcli elisseti
      pire cz 1z cc0 csn cdif cv csin cfv cdiv coeq1i wf wbr copab eleq1 opreq2
      cmpt wa eqeq2d anbi12d cbvopab1v eqtri wne a1i nnne0 redivcl syl111anc cc
      nnre recnd pipos gt0ne0ii nncn syl22anc eldifsn sylanbrc fopab circumlem2
      divne0 sindivcvg mp2an eqbrtri oprex df-mpt fnopab2 wfun resincl 3anidm12
      syl3an1 sylbi ffun ax-mp fvco3 mp3an12 ffvelrn sylancr eqeltrd circumlem1
      mpan fvopab4 fveq2d fveq2 id opreq12d syl 3eqtrd opreq2d eqtr4d climmulci
      jca mulid1i breqtri ) DUBNOPZEOPZUCOPYCUDYCUCUAGHUEZDYBEUBNUFNUJUGUHEMUGZ
      UHZUCUKULUIYDAQUMUNUOZAUPZUQURZYHUSPZVFZHUEZUCUDGYKHKUTRYGHVAZHUMUDVBYLUC
      UDVBUABRYGNUAUPZUSPZHHFUPZRSZBUPZNYPUSPZTZVGZFBVCYNRSZYRYOTZVGZUABVCLUUAU
      UDFBUAYPYNTZYQUUBYTUUCYPYNRVDUUEYSYOYRYPYNNUSVEZVHVIVJVKUUBYOQSZYOUMVLZYO
      YGSZUUBNQSZYNQSYNUMVLZUUGUUJUUBUJVMZYNVRYNVNZNYNVOVPUUBNVQSNUMVLZYNVQSUUK
      UUHUUBNUULVSUUNUUBNUJVTWAVMYNWBUUMNYNWHWCYOQUMWDWEZWFZABCDEFGHIJKLWGAHWIW
      JWKFBRUBYPOPZECUBUSPUQUROPZOPZDUUQUUROWLDFRUUSVFYQYRUUSTVGFBVCJFBRUUSWMVK
      WNUUBYNYDURZVQSYNDURZYCUUTOPZTUUBUUTYNHURZGURZVQGWOZYMUUBUUTUVDTYGQGVAZUV
      EABYGQYJGGYKYHYGSZYRYJTVGABVCKABYGYJWMVKZUVGYHQSZYHUMVLZVGYJQSZYHQUMWDUVI
      UVJUVKUVIYIQSUVIUVJUVKYHWPYIYHVOWRWQWSWFZYGQGWTXAUUPRYGYNGHXBXCZUUBUVDUUB
      UVFUVCYGSZUVDQSUVLYMUUBUVNUUPRYGYNHXDXHYGQUVCGXDXEVSXFUUBUVAYCYOUQURZYOUS
      PZOPZUVBEVQSUUBUVAUVQTYEUACDEFIJXGXHUUBUUTUVPYCOUUBUUTUVDYOGURZUVPUVMUUBU
      VCYOGFBYNYSYORHUUFLNYNUSWLXIXJUUBUUIUVRUVPTUUOABYOYJUVPYGGYHYOTZYIUVOYHYO
      USYHYOUQXKUVSXLXMUVHUVOYOUSWLXIXNXOXPXQXSXRYCYFXTYA $.
      $( [10-Nov-2012] $)
  $}

  ${
    $d A y $.  $d R n y $.
    circum.1 $e |- A = ( ( 2 x. pi ) / n ) $.
    circum.2 $e |-
      P = ( n e. NN |-> ( ( 2 x. n ) x. ( R x. ( sin ` ( A / 2 ) ) ) ) ) $.
    circum.3 $e |- R e. RR $.
    $( The circumference of a circle of radius ` R ` , defined as the limit as
       ` n ~~> +oo ` of the perimeter of an inscribed n-sided isogons, is
       ` ( ( 2 x. pi ) x. R ) ` .  (Contributed by Paul Chapman,
       10-Nov-2011.) $)
    circum $p |- P ~~> ( ( 2 x. pi ) x. R ) $=
      ( vy cr cc0 csn cdif cv csin cfv cdiv co cmpt cn eqid wcel cpi wceq copab
      wa circumlem3 ) DHABCDDIJKLDMZNOUGPQRZUGSUAHMUBUGPQUCUEDHUDZEFUHTUITGUF
      $.
      $( [10-Nov-2012] $)
  $}

