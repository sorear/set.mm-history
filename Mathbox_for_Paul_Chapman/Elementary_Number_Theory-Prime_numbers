$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Elementary Number Theory - Prime numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Prime $.

  $( Extend the definition of a class to include the set of prime numbers. $)
  cprime $a class Prime $.

  ${
    $d n p $.
    $( Define the set of prime numbers. $)
    df-prime $a |- Prime = { p e. NN | { n e. NN | n || p } ~~ 2o } $.
  $}

  ${
    $d P n p $.
    $( The predicate "is a prime number".  A prime number is a positive integer
       with exactly two positive divisors.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    isprm $p |- ( P e. Prime <->
                  ( P e. NN /\ { n e. NN | n || P } ~~ 2o ) ) $=
      ( vp cv cdivides wbr cn crab c2o cen cprime wceq wb wcel breq2 adantr
      rabbidva breq1d df-prime elrab2 ) BDZCDZEFZBGHZIJFUAAEFZBGHZIJFCAGKUBALZU
      DUFIJUGUCUEBGUGUCUEMUAGNUBAUAEOPQRBCST $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d n z $.
    $( ` 1 ` is not a prime number.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    1nprm $p |- 1 e/ Prime $=
      ( vn vz c1 cprime wnel wcel wn cv cdivides wbr cn crab c2o cen csdm c1o
      csn wa wceq cle cz wi nnz 1nn dvdsle mpan2 syl nnge1 a1d jcad cr wb nnre
      1re letri3 sylibrd imp 1z iddvds ax-mp pm3.2i eleq1 breq1 anbi12d mpbiri
      impbii elrab elsn 3bitr4i eqriv elisseti ensn1 eqbrtri 1sdom2 ensdomtr
      mp2an sdomnen isprm mpbiran mtbir df-nel mpbir ) CDECDFZGWCAHZCIJZAKLZMNJ
      ZWFMOJZWGGWFPNJPMOJWHWFCQZPNBWFWIBHZKFZWJCIJZRZWJCSZWJWFFWJWIFWMWNWKWLWNW
      KWLWJCTJZCWJTJZRZWNWKWLWOWPWKWJUAFZWLWOUBZWJUCWRCKFZWSUDWJCUEUFUGWKWPWLWJ
      UHUIUJWKWJUKFZWNWQULZWJUMXACUKFXBUNWJCUOUFUGUPUQWNWMWTCCIJZRWTXCUDCUAFXCU
      RCUSUTVAWNWKWTWLXCWJCKVBWJCCIVCVDVEVFWEWLAWJKWDWJCIVCVGBCVHVIVJCCKUDVKVLV
      MVNWFPMVOVPWFMVQUTWCWTWGCAVRUDVSVTCDWAWB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d N n $.
    $( The positive divisors of a positive integer include ` 1 ` and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    1idssfct $p |- ( N e. NN -> { 1 , N } C_ { n e. NN | n || N } ) $=
      ( c1 cv cdivides wbr cn crab wcel cpr wss wa prssg ibi cz nnz 1dvds 1nn
      breq1 elrab biimpri mpan 3syl iddvds syl mpdan sylanc ) CADZBEFZAGHZIZBUJ
      IZCBJUJKZBGIZUKULLUMCBUJUJUJMNUNBOIZCBEFZUKBPZBQCGIZUPUKRUKURUPLUIUPACGUH
      CBESTUAUBUCUNBBEFZULUNUOUSUQBUDUEULUNUSLUIUSABGUHBBESTUAUFUG $.
      $( [22-Jun-2011] $)
  $}

  $( Two ways to say "an integer greater than ` 1 ` ."  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zgt1b1 $p |- ( ( N e. ZZ /\ 1 < N ) <-> ( N e. NN /\ N =/= 1 ) ) $=
    ( cz wcel c1 clt wbr wa cn wne cc0 wi lt01 cr zre 0re 1re lttr mp3an12 syl
    exp3a mpi imp elnnz biimpri syldan pm3.27 jca nnz adantr impbii cle wb
    leltne mp3an1 nnre nnge1 sylanc pm5.32i bitri ) ABCZDAEFZGZAHCZVAGZVCADIZGV
    BVDVBVCVAUTVAJAEFZVCUTVAVFUTJDEFZVAVFKLUTVGVAVFUTAMCZVGVAGVFKZANJMCDMCZVHVI
    OPJDAQRSTUAUBVCUTVFGAUCUDUEUTVAUFUGVDUTVAVCUTVAAUHUIVCVAUFUGUJVCVAVEVHDAUKF
    ZVAVEULZVCVJVHVKVLPDAUMUNAUOAUPUQURUS $.
    $( [22-Jun-2011] $)

  $( Two ways to say "an integer greater than ` 1 ` ."  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zgt1b2 $p |- ( ( N e. ZZ /\ 1 < N ) <-> N e. ( ZZ>= ` 2 ) ) $=
    ( cz wcel c1 clt wbr wa c2 cle cuz cfv caddc co wb 1z zltp1le mpan 1p1e2
    breq1i syl6bb pm5.32i w3a 3anass eluz2 2z biantrur 3bitr4i bitr4i ) ABCZDAE
    FZGUIHAIFZGZAHJKCZUIUJUKUIUJDDLMZAIFZUKDBCUIUJUONODAPQUNHAIRSTUAHBCZUIUKUBU
    PULGUMULUPUIUKUCHAUDUPULUEUFUGUH $.
    $( [22-Jun-2011] $)

  $( Two ways to say "an integer greater than ` 1 ` ."  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zgt1b3 $p |- ( ( N e. ZZ /\ 1 < N ) <-> ( N e. NN /\ 1 < N ) ) $=
    ( cz wcel c1 clt wbr wa cn wne zgt1b1 pm3.26bi pm3.27 jca nnz anim1i
    impbii ) ABCZDAEFZGZAHCZRGSTRSTADIAJKQRLMTQRANOP $.
    $( [22-Jun-2011] $)

  ${
    $d P n p $.
    $( Lemma for ~ isprm2 . $)
    isprm2lem $p |- ( ( P e. NN /\ P =/= 1 ) ->
      ( { n e. NN | n || P } ~~ 2o <-> { n e. NN | n || P } = { 1 , P } ) ) $=
      ( vp cn wcel c1 wne cv cdivides wbr crab c2o cen cpr wceq wb wi neeq1
      breq2 rabbidv breq1d preq2 eqeq12d bibi12d imbi12d wa wss cfn prfi cvv
      nnex ssrab2 ssexi enfi mpan mpbii adantl wn wpss pssinf dfpss2 sylanbr
      an1rs ex con4d mpd 1idssfct entr com 2onn elisseti ensym sylan2 csn cun
      cin c0 disjsn c1o 1nn ensn1 visset pm54.43 mp2an elsnc notbii 3bitr3ri
      df-ne df-pr breq1i 3bitr4i sylanb syl2an eqcomd anassrs breq1 syl6bbr
      biimprcd impbid rgen vtoclri imp ) ADEAFGZBHZAIJZBDKZLMJZXFFANZOZPZCHZFGZ
      XDXKIJZBDKZLMJZXNFXKNZOZPZQZXCXJQCADXKAOZXLXCXRXJXKAFRXTXOXGXQXIXTXNXFLMX
      TXMXEBDXKAXDISTZUAXTXNXFXPXHYAXKAFUBUCUDUEXSCDXKDEZXLXRYBXLUFZXOXQYCXOXQY
      BXLXOXQYBXLXOUFZUFXPXNXPXNUGZXPXNMJZXPXNOZYBYDYEYFUFZXNUHEZYGYFYIYEYFXPUH
      EZYIFXKUIXNUJEYFYJYIPXNDUKXMBDULUMXPXNUJUNUOUPUQYHYGYIYHYGURZYIURZYEYKYFY
      LXPXNUSYFYLYEYKUFXPXNUTXPXNVAVBVCVDVEVFBXKVGXPLMJZXOYFXLYMLXNMJYFXOXPLXNV
      HXNLLVIVJVKVLVMXKFOZURZFVNZXKVNZVOZLMJZXLYMYPYQVPVQOZXKYPEZURYSYOYPXKVRYP
      VSMJYQVSMJYTYSPFFDVTVKWAXKCWBZWAYPYQWCWDUUAYNXKFUUBWEWFWGXKFWHXPYRLMFXKWI
      WJWKZWLWMWNWOVDXLXQXOQYBXQXOXLXQXOYMXLXNXPLMWPUUCWQWRUQWSVDWTXAXB $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d P n z $.
    $( The predicate "is a prime number".  A prime number is an integer greater
       than ` 1 ` whose only positive divisors are ` 1 ` and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    isprm2 $p |- ( P e. Prime <-> ( P e. ZZ /\ 1 < P /\
                   A. z e. NN ( z || P -> ( z = 1 \/ z = P ) ) ) ) $=
      ( vn cprime wcel cz c1 clt wbr wa cv cdivides wceq wo wi cn wral w3a
      crab cpr wss wne wn wnel 1nprm df-nel mpbi eleq1 biimpcd mtoi df-ne
      sylibr pm4.71i c2o cen wb isprm2lem jcab eqss imbi2i 1idssfct biantru
      3bitr4i pm5.74ri adantr bitrd expcom pm5.32d isprm syl5bb pm5.32ri bitri
      ancom anass zgt1b1 bitr4i anbi1i 3bitr2i wal breq1 elrab visset elpr
      imbi12i impexp albii dfss2 df-ral anbi2i df-3an ) BDEZBFEZGBHIZJZAKZBLIZW
      OGMWOBMNZOZAPQZJZWLWMWSRWKWNCKZBLIZCPSZGBTZUAZJZWTWKBPEZXEJZBGUBZJZXFWKWK
      XIJXJWKXIWKBGMZUCXIWKXKGDEZGDUDXLUCUEGDUFUGXKWKXLBGDUHUIUJBGUKULUMXIWKXHX
      IXGXCUNUOIZJXHWKXIXGXMXEXGXIXMXEUPXGXIJZXMXCXDMZXEBCUQXGXOXEUPXIXGXOXEXGX
      EXDXCUAZJZOXGXEOZXGXPOZJXGXOOXRXGXEXPURXOXQXGXCXDUSUTXSXRCBVAVBVCVDVEVFVG
      VHBCVIVJVKVLXJXIXHJXIXGJZXEJXFXHXIVMXIXGXEVNXTWNXEXTXNWNXIXGVMBVOVPVQVRVL
      XEWSWNWOXCEZWOXDEZOZAVSWOPEZWROZAVSXEWSYCYEAYCYDWPJZWQOYEYAYFYBWQXBWPCWOP
      XAWOBLVTWAWOGBAWBWCWDYDWPWQWEVLWFAXCXDWGWRAPWHVCWIVLWLWMWSWJVP $.
      $( [22-Jun-2011] $)

    $( The predicate "is a prime number".  A prime number is an integer greater
       than ` 1 ` with no divisors strictly between ` 1 ` and itself.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    isprm3 $p |- ( P e. Prime <-> ( P e. ZZ /\ 1 < P /\
                   A. z e. ( 2 ... ( P - 1 ) ) -. z || P ) ) $=
      ( cprime wcel cz c1 clt wbr cv cdivides wceq wo wi cn wral w3a wn c2
      cmin co cfz isprm2 wa wb cle nnge1 adantr a1d dvdsle nnz sylan jcad cc0
      lt01 cr zre 0re 1re lttr mp3an12 syl mpani elnnz pm3.26bi2 syld imp
      sylan2 wne leltne mp3an1 3adant2 3expia anim12d syl2an pm4.38 df-ne
      necom bitri anbi12i ioran bitr4i bibi2i sylib syl6 adantrr caddc 1z
      zltp1le mpan 1p1e2 breq1i syl6bb zltlem1 anbi12d 2z elfz mp3an2 peano2zm
      bitr4d bitr3d anasss expcom pm5.32d cuz cfv fzssuz wss 2re 1lt2 ltleii
      eluz mp2an mpbir uzss ax-mp sstri nnuz sseqtr4i sseli pm4.71ri syl6bbr
      notbid iman syl5bb ex pm5.74d bi2.04 syl5bbr con2b ralbidv2 pm5.32i
      df-3an 3bitr4i ) BCDBEDZFBGHZAIZBJHZYPFKZYPBKZLZMZANOZPZYNYOYQQZARBFSTZUA
      TZOZPZABUBYNYOUCZUUBUCUUIUUGUCUUCUUHUUIUUBUUGUUIUUAUUDANUUFUUIYPNDZUUAMZY
      QYPUUFDZQZMZUULUUDMUUIYQUUJYTMZMUUNUUKUUIYQUUOUUMUUIYQUUOUUMUDUUIYQUCZUUJ
      YTQZUCZQUUMUUOUUPUURUULUUPUURUUJUULUCUULUUPUUJUUQUULUUJUUPUUQUULUDZUUJUUI
      YQUUSUUJUUIUCZYQUCFYPGHZYPBGHZUCZUUQUULUUTYQUVCUUQUDZUUTYQFYPUEHZYPBUEHZU
      CZUVDUUJBNDZYQUVGMUUIUUJUVHUCZYQUVEUVFUVIUVEYQUUJUVEUVHYPUFUGUHYPEDZUVHYQ
      UVFMUUJYPBUIYPUJZUKULYNYOUVHYNYOUMBGHZUVHYNUMFGHZYOUVLUNYNBUODZUVMYOUCUVL
      MZBUPZUMUODFUODZUVNUVOUQURUMFBUSUTVAVBUVHYNUVLBVCVDVEVFVGUUJYNUVGUVDMZYOU
      VJYNUVRUUJUVJYNUCZUVGUVAYPFVHZUDZUVBBYPVHZUDZUCZUVDYPUODZUVNUVGUWDMUVJYNU
      WEUVNUCUVEUWAUVFUWCUWEUVNUVEUWAUWEUVEUWAUVNUVQUWEUVEUWAURFYPVIVJVKVLUWEUV
      NUVFUWCYPBVIVLVMYPUPUVPVNUWDUVCUVTUWBUCZUDUVDUVAUVBUVTUWBVOUWFUUQUVCUWFYR
      QZYSQZUCUUQUVTUWGUWBUWHYPFVPUWBYPBVHUWHBYPVQYPBVPVRVSYRYSVTWAWBWCWDUVKUKW
      EVEVFUUTUVCUULUDZYQUUJYNUWIYOUVJYNUWIUUJUVSUVCRYPUEHZYPUUEUEHZUCZUULUVSUV
      AUWJUVBUWKUVJUVAUWJUDYNUVJUVAFFWFTZYPUEHZUWJFEDZUVJUVAUWNUDWGFYPWHWIUWMRY
      PUEWJWKWLUGYPBWMWNUVJUUEEDZUULUWLUDZYNUVJREDZUWPUWQWOYPRUUEWPWQBWRVGWSUVK
      UKWEUGWTXAXBXCUULUUJUUFNYPUUFFXDXEZNUUFRXDXEZUWSRUUEXFRUWSDZUWTUWSXGUXAFR
      UEHZFRURXHXIXJUWOUWRUXAUXBUDWGWOFRXKXLXMFRXNXOXPXQXRXSXTYAYBUUJYTYCYDYEYF
      YQUUJYTYGYHYQUULYIWLYJYKYNYOUUBYLYNYOUUGYLYMVR $.
      $( [22-Jun-2011] $)

    $( A prime number is an integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmz $p |- ( P e. Prime -> P e. ZZ ) $=
      ( vz cprime wcel cz c1 clt wbr cv cdivides wceq wo wi cn wral isprm2
      3simp1bi ) ACDAEDFAGHBIZAJHRFKRAKLMBNOBAPQ $.
      $( [22-Jun-2011] $)

    $( A prime number is a positive integer.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    prmnn $p |- ( P e. Prime -> P e. NN ) $=
      ( vz cprime wcel cn cv cdivides wbr crab c2o cen wa isprm biimpi pm3.26d
      ) ACDZAEDZBFAGHBEIJKHZPQRLABMNO $.
      $( [22-Jun-2011] $) $( [26-Jun-2011] $)
  $}

  $( ` 2 ` is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  2prm $p |- 2 e. Prime $=
    ( vz c2 cz wcel c1 clt wbr cv cdivides wn cmin co cfz wral cprime 2z 1lt2
    c0 ral0 wceq caddc 1p1e2 2cn ax1cn subaddi mpbir eqbrtri wb 1z eqeltri fzn
    mp2an mpbi raleqi w3a isprm3 biimpri mp3an ) BCDZEBFGZAHBIGJZABBEKLZMLZNZBO
    DZPQVDVAARNVAASVAAVCRVBBFGZVCRTZVBEBFVBETEEUALBTUBBEEUCUDUDUEUFZQUGUSVBCDVF
    VGUHPVBECVHUIUJBVBUKULUMUNUFVEUSUTVDUOABUPUQUR $.
    $( [22-Jun-2011] $)

  $( ` 3 ` is a prime number.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  3prm $p |- 3 e. Prime $=
    ( vz c3 cz wcel c1 clt wbr cv cdivides wn c2 cmin co cfz wral cprime cn
    3nn nnz ax-mp 1lt3 wceq caddc df-3 eqcomi cc nncn ax1cn 2cn subadd2i mpbir
    opreq2i eleq2i wi 2z iddvds 2nn 1lt2 ndvdsp1 mp3an breq2i mtbir breq1
    mtbiri elfz1eq elfz3 eleq1 mpbiri impbii imbi1i sylbi rgen w3a isprm3
    biimpri ) BCDZEBFGZAHZBIGZJZAKBELMZNMZOZBPDZBQDZVPRBSTUAVTAWBVRWBDVRKKNMZDZ
    VTWBWFVRWAKKNWAKUBKEUCMZBUBBWHUDUEBEKWEBUFDRBUGTUHUIUJUKULUMWGVTUNVRKUBZVTU
    NWIVSKBIGZWJKWHIGZKKIGZWKJZKCDZWLUOKUPTWNKQDEKFGWLWMUNUOUQURKKUSUTTBWHKIUDV
    AVBVRKBIVCVDWGWIVTWGWIVRKVEWIWGKWFDZWNWOUOKVFTVRKWFVGVHVIVJUKVKVLWDVPVQWCVM
    ABVNVOUT $.
    $( [22-Jun-2011] $)

  $( ` 4 ` is not a prime number.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  4nprm $p |- 4 e/ Prime $=
    ( vz c4 cprime wnel wcel wn cv cdivides wbr c2 c1 cmin co cfz wral wrex c3
    cuz cfv cle 2re 3re 2lt3 ltleii cz wb 2z cn 3nn nnz ax-mp eluz mp2an mpbir
    wceq caddc df-4 eqcomi cc 4nn nncn ax1cn subadd2i eleq1i eluzfz1 sylbir
    cmul dvdsmul1 2t2e4 breqtri breq1 rcla4ev dfrex2 mpbi clt isprm3 3simp3bi
    mto df-nel ) BCDBCEZFVTAGZBHIZFAJBKLMZNMZOZWBAWDPZWEFJWDEZJBHIZWFQJRSZEZWGW
    JJQTIZJQUAUBUCUDJUEEZQUEEZWJWKUFUGQUHEZWMUIQUJUKJQULUMUNWJWCWIEWGWCQWIWCQUO
    QKUPMZBUOBWOUQURBKQBUHEBUSEUTBVAUKVBWNQUSEUIQVAUKVCUNVDJWCVEVFUKJJJVGMZBHWL
    WLJWPHIUGUGJJVHUMVIVJWBWHAJWDWAJBHVKVLUMWBAWDVMVNVTBUEEKBVOIWEABVPVQVRBCVSU
    N $.
    $( [22-Jun-2011] $)

  ${
    $d N z $.  $d P z $.
    $( A prime number either divides an integer or is coprime to it, but not
       both.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    coprm $p |- ( ( P e. Prime /\ N e. ZZ ) ->
                  ( -. P || N <-> ( P gcd N ) = 1 ) ) $=
      ( vz cprime wcel cz wa cdivides wbr wn cgcd co c1 wceq breq1 gcddvds clt
      cv wo wi cn wral isprm2 3simp1bi sylan pm3.27d syl5cbi con3d biorf orcom
      syl6bb pm3.26d cc0 0nnn prmnn eleq1 biimpcd syl mtoi intnanrd adantr
      gcdn0cl ex mpd eqeq1 orbi12d imbi12d rcla4v 3simp3bi syl5 com12 syl5cbir
      syld wne cle iddvds w3a dvdslegcd 3anidm12 mpand 3simp2bi cr 1re ltletr
      mp3an1 zre nnre sylanc ltne 3expia mpan 3syld df-ne syl6ib con2d impbid
      ) ADEZBFEZGZABHIZJZABKLZMNZWSXAXBANZJZXCWSXDWTXDXBBHIZWTWSXBABHOWSXBAHIZX
      FAFEZWRXGXFGWQABPWQXHMAQIZCRZAHIZXJMNZXJANZSZTZCUAUBZCAUCZUDZUEZUFUGUHXEX
      CXCXDSZWSXEXCXDXCSXTXDXCUIXDXCUJUKWSXGXTWSXGXFXSULWSXBUAEZXGXTTZWSAUMNZBU
      MNZGJZYAWQYEWRWQYCYDWQYCUMUAEZUNWQAUAEZYCYFTAUOYCYGYFAUMUAUPUQURUSUTVAZXH
      WRYEYATWQXHWRGYEYAABVBVCXRUEVDZWQYAYBTWRYAWQYBYAXPYBWQXOYBCXBUAXJXBNZXKXG
      XNXTXJXBAHOYJXLXCXMXDXJXBMVEXJXBAVEVFVGVHWQXHXIXPXQVIVJVKVAVDVDVLVMWSWTXC
      WSWTXBMVNZXCJWSWTAXBVOIZMXBQIZYKWSAAHIZWTYLWQYNWRWQXHYNXRAVPURVAWSYEYNWTG
      YLTZYHXHWRYEYOTZWQXHWRYPXHXHWRVQYEYOAABVRVCVSXRUEVDVTWSXIYLYMWQXIWRWQXHXI
      XPXQWAVAAWBEZXBWBEZXIYLGYMTZWSMWBEZYQYRYSWCMAXBWDWEWQYQWRWQXHYQXRAWFURVAW
      SYAYRYIXBWGURZWHVTWSYRYMYKTZUUAYTYRUUBWCYTYRYMYKMXBWIWJWKURWLXBMWMWNWOWP
      $.
      $( [22-Jun-2011] $)
  $}

  $( If an integer divides the product of two integers and is coprime to one of
     them, then it divides the other.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  coprmdvds $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                    ( ( K || ( M x. N ) /\ ( K gcd M ) = 1 ) -> K || N ) ) $=
    ( cz wcel w3a cmul co cdivides wbr cgcd c1 wceq wi wa cc mulcom zcn syl2an
    3adant1 breq2d dvdsmul2 ancoms 3adant2 dvdsgcd 3simp1 zmulcl syl3anc mpand
    sylbid adantr cabs cfv absmulgcd 3coml opreq2 mulid1 syl sylan9eqr fveq2d
    3ad2antl3 eqtrd wb dvdsabsb bitr4d sylibd ex com23 imp3a ) ADEZBDEZCDEZFZAB
    CGHZIJZABKHZLMZACIJZVMVQVOVRVMVQVOVRNVMVQOZVOACAGHZCBGHZKHZIJZVRVMVOWCNVQVM
    VOAWAIJZWCVMVNWAAIVKVLVNWAMZVJBPECPEZWEVKVLBCQBRCRZSTUAVMAVTIJZWDWCVJVLWHVK
    VLVJWHCAUBUCUDVJVTDEZWADEZWHWDOWCNVMAVTWAUEVJVKVLUFVJVLWIVKVLVJWICAUGUCUDVK
    VLWJVJVLVKWJCBUGUCTUHUIUJUKVSWCACULUMZIJZVRVSWBWKAIVSWBCVPGHZULUMZWKVMWBWNM
    ZVQVLVJVKWOCABUNUOUKVLVJVQWNWKMVKVLVQOWMCULVQVLWMCLGHZCVPLCGUPVLWFWPCMWGCUQ
    URUSUTVAVBUAVMVRWLVCZVQVJVLWQVKACVDUDUKVEVFVGVHVI $.
    $( [22-Jun-2011] $)

  $( Euclid's lemma.  If a prime number divides the product of two integers, it
     divides at least one of them.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  prmdvdsmul $p |- ( ( P e. Prime /\ M e. ZZ /\ N e. ZZ ) ->
                     ( P || ( M x. N ) -> ( P || M \/ P || N ) ) ) $=
    ( cprime wcel cz w3a cmul co cdivides wbr wn wi wo wa cgcd c1 wceq wb
    coprm 3adant3 anbi2d coprmdvds prmz syl3an1 sylbid exp3a df-or syl6ibr ) AD
    EZBFEZCFEZGZABCHIJKZABJKZLZACJKZMUOUQNUMUNUPUQUMUNUPOUNABPIQRZOZUQUMUPURUNU
    JUKUPURSULABTUAUBAFEUKULUSUQMUJABCUCAUDUEUFUGUOUQUHUI $.
    $( [22-Jun-2011] $)

$( (End of Paul Chapman's mathbox.) $)


