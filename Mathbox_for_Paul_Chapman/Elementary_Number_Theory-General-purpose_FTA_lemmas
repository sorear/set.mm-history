$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Elementary Number Theory - General-purpose FTA lemmas
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d B x $.  $d ps x $.
    ralunsn.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Restricted quantification over the union of a set and a singleton, using
       implicit substitution.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
    ralunsn $p |- ( B e. C -> ( A. x e. ( A u. { B } ) ph <->
                                ( A. x e. A ph /\ ps ) ) ) $=
      ( wcel csn cun wral wa wb ralunb a1i wsbc ralsng sbcieg bitrd anbi2d ) EF
      HZACDEIZJKZACDKZACUBKZLZUDBLUCUFMUAACDUBNOUAUEBUDUAUEACEPBACEFQABCEFGRSTS
      $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d C x $.  $d ch x $.  $d ps y $.  $d th x $.
    2ralunsn.1 $e |- ( x = B -> ( ph <-> ch ) ) $.
    2ralunsn.2 $e |- ( y = B -> ( ph <-> ps ) ) $.
    2ralunsn.3 $e |- ( x = B -> ( ps <-> th ) ) $.
    $( Double restricted quantification over the union of a set and a
       singleton, using implicit substitution.  (Contributed by Paul Chapman,
       17-Nov-2012.) $)
    2ralunsn $p |- ( B e. C ->
                     ( A. x e. ( A u. { B } ) A. y e. ( A u. { B } ) ph <->
                       ( ( A. x e. A A. y e. A ph /\ A. x e. A ps ) /\
                         ( A. y e. A ch /\ th ) ) ) ) $=
      ( wcel csn cun wral wa ralunsn ralbidv cv wceq anbi12d r19.26 anbi1i
      syl6bb bitrd ) HIMZAFGHNOZPZEUHPAFGPZBQZEUHPZUJEGPBEGPQZCFGPZDQZQZUGUIUKE
      UHABFGHIKRSUGULUKEGPZUOQUPUKUOEGHIETHUAZUJUNBDURACFGJSLUBRUQUMUOUJBEGUCUD
      UEUF $.
      $( [17-Nov-2012] $)
  $}

  $( Membership in a curtailed finite sequence of integers.  (Contributed by
     Paul Chapman, 17-Nov-2012.) $)
  elfzm12 $p |- ( N e. NN ->
                  ( M e. ( 1 ... ( N - 1 ) ) -> M e. ( 1 ... N ) ) ) $=
    ( cn wcel c1 cmin co cfz cuz cfv wss cz nnz cle wbr cr zre caddc lep1 wb
    1re lesubadd mp3an2 anidms mpbird syl peano2zm eluz mpancom 1z fzss2 mpan2
    sseld ) BCDZEBEFGZHGZEBHGZAUNBUOIJDZUPUQKZUNBLDZURBMUTURUOBNOZUTBPDZVABQVBV
    ABBERGNOZBSVBVAVCTZVBEPDVBVDUABEBUBUCUDUEUFUOLDUTURVATBUGUOBUHUIUEUFURELDUS
    UJUOEBUKULUFUM $.
    $( [17-Nov-2012] $)

  $( An integer greater than 2 is a positive integer.  (Contributed by Paul
     Chapman, 17-Nov-2012.) $)
  uz2nn $p |- ( N e. ( ZZ>= ` 2 ) -> N e. NN ) $=
    ( cn wcel cz c1 cle wbr c2 cuz cfv elnnz1 eluzelz cr zre syl 1re 2re 1lt2
    ltleii a1i eluzle wa wi letr mp3an12 imp syl12anc sylanbrc ) ABCADCZEAFGZAH
    IJCZAKHALZUKAMCZEHFGZHAFGZUJUKUIUMULANOUNUKEHPQRSTHAUAUMUNUOUBZUJEMCHMCUMUP
    UJUCPQEHAUDUEUFUGUH $.
    $( [17-Nov-2012] $)

  $( A positive integer is either 1 or greater than or equal to 2.
     (Contributed by Paul Chapman, 17-Nov-2012.) $)
  elnn1uz2 $p |- ( N e. NN <-> ( N = 1 \/ N e. ( ZZ>= ` 2 ) ) ) $=
    ( cn wcel c1 wceq c2 cuz cfv wo clt wbr cle nnge1 cr wb nnre 1re leloe
    mpan syl mpbid cz wa zgt1b2 zgt1b3 bitr3i baib eqcom a1i orbi12d mpbird
    orcom sylib 1nn eleq1 mpbiri uz2nn jaoi impbii ) ABCZADEZAFGHCZIZUTVBVAIZVC
    UTVDDAJKZDAEZIZUTDALKZVGAMUTANCZVHVGOZAPDNCVIVJQDARSTUAUTVBVEVAVFVBUTVEVBAU
    BCVEUCUTVEUCAUDAUEUFUGVAVFOUTADUHUIUJUKVBVAULUMVAUTVBVAUTDBCUNADBUOUPAUQURU
    S $.
    $( [17-Nov-2012] $)

  ${
    $d N x $.
    $( Lemma for ~ fseq1p1m1 . $)
    fseq1p1m1lem1 $p |- ( N e. NN0 ->
                          ( ( 1 ... N ) i^i { ( N + 1 ) } ) = (/) ) $=
      ( vx cn0 wcel cv c1 caddc co csn wn cfz wral cin c0 wceq cle wbr wi elsn
      breq1 biimpd cr nn0re clt ltp1 wb peano2re ltnle mpdan mpbid syl nsyli
      sylbi com12 con2d elfzle2 syl5 r19.21aiv disj sylibr ) ACDZBEZAFGHZIZDZJZ
      BFAKHZLVGVDMNOVAVFBVGVAVBAPQZVFVBVGDVAVEVHVEVAVHJZVEVBVCOZVAVIRBVCSVJVHVC
      APQZVAVJVHVKVBVCAPTUAVAAUBDZVKJZAUCVLAVCUDQZVMAUEVLVCUBDVNVMUFAUGAVCUHUIU
      JUKULUMUNUOVBFAUPUQURBVGVDUSUT $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1p1m1lem2.1 $e |- H = { <. ( N + 1 ) , B >. } $.
    fseq1p1m1lem2.2 $e |- N e. NN0 $.
    $( Lemma for ~ fseq1p1m1 . $)
    fseq1p1m1lem2 $p |- ( ( G : ( 1 ... ( N + 1 ) ) --> A /\
                          ( G ` ( N + 1 ) ) = B /\ F = ( G |` ( 1 ... N ) ) )
        ->
        ( F : ( 1 ... N ) --> A /\ B e. A /\ G = ( F u. H ) ) ) $=
      ( c1 caddc co cfz wf cfv wceq cres w3a wcel cun wss cz 1z cn0 nn0z ax-mp
      fzssp1 mp2an fssres mpan2 3ad2ant1 wb feq1 3ad2ant3 mpbird simp2 cuz cn
      nn0p1nn elnnuz mpbi eluzfz2 ffvelrn eqeltrrd csn simp3 cop wfn simp1 ffn
      syl fnressn opeq2 sneqd eqtrd syl6eqr eqcomd uneq12d fnresdm cle wbr
      nnge1 fzsuc mp3an reseq2i resundi eqtri syl5eqr eqtr2d 3jca ) IFIJKZLKZAD
      MZWJDNZBOZCDIFLKZPZOZQZWOACMZBARDCESZOWRWSWOAWPMZWLWNXAWQWLWOWKTZXAIUARZF
      UARZXBUBFUCRZXDHFUDUEZIFUFUGWKAWODUHUIUJWQWLWSXAUKWNWOACWPULUMUNWRWMBAWLW
      NWQUOZWLWNWMARZWQWLWJWKRZXHWJIUPNRZXIWJUQRZXJXEXKHFURUEZWJUSUTIWJVAUEZWKA
      WJDVBUIUJVCWRWTWPDWJVDZPZSZDWRCWPEXOWLWNWQVEWRXOEWRXOWJBVFZVDZEWRXOWJWMVF
      ZVDZXRWRDWKVGZXOXTOZWRWLYAWLWNWQVHWKADVIVJZYAXIYBXMWKWJDVKUIVJWRWNXTXROXG
      WNXSXQWMBWJVLVMVJVNGVOVPVQWRYAXPDOYCYADWKPZDXPWKDVRYDDWOXNSZPXPWKYEDXCXDI
      WJVSVTZWKYEOUBXFXKYFXLWJWAUEIFWBWCWDDWOXNWEWFWGVJWHWI $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1p1m1lem3.1 $e |- H = { <. ( N + 1 ) , B >. } $.
    fseq1p1m1lem3.2 $e |- N e. NN0 $.
    $( Lemma for ~ fseq1p1m1 . $)
    fseq1p1m1lem3 $p |- ( ( F : ( 1 ... N ) --> A /\ B e. A /\
      G = ( F u. H ) ) -> ( G : ( 1 ... ( N + 1 ) ) --> A /\
      ( G ` ( N + 1 ) ) = B /\ F = ( G |` ( 1 ... N ) ) ) ) $=
      ( c1 cfz co wf wcel cun wceq w3a caddc cfv cres csn simp1 simp2 wss cn
      cn0 nn0p1nn ax-mp wa cop fsng mpbiri mpan snssi fss syl11anc syl cin c0
      fseq1p1m1lem1 fun 3impa mp3an3 wb cz cle wbr 1z nn0z nnge1 fzsuc mp3an
      unidm eqcomi feq23 mp2an sylibr simp3 feq1d mpbird reseq1 wfn ffn
      fnresdisj mpbii uneq1d uncom un0 eqtri syl6eq resundir syl5eq 3syl
      fnresdm 3eqtrd fveq1d a1i fvsng fveq1i eqtrd oprex snid fvres syl5eqr
      incom uneq2d 3eqtrrd 3jca ) IFJKZACLZBAMZDCENZOZPZIFIQKZJKZADLZXNDRZBOCDX
      HSZOXMXPXOAXKLZXMXHXNTZNZAANZXKLZXSXMXIXTAELZYCXIXJXLUAZXMXJYDXIXJXLUBZXJ
      XTBTZELZYGAUCYDXNUDMZXJYHFUEMZYIHFUFUGZYIXJUHZYHEXNBUITZOZGXNBUDAEUJZUKUL
      BAUMXTYGAEUNUOUPZXIYDXHXTUQZUROZYCYJYRHFUSUGZXIYDYRYCXHXTAACEUTVAVBUOXOYA
      OZAYBOXSYCVCIVDMFVDMZIXNVEVFZYTVGYJUUAHFVHUGYIUUBYKXNVIUGIFVJVKYBAAVLVMXO
      AYAYBXKVNVOVPXMXOADXKXIXJXLVQZVRVSXMXNDXTSZRZBXQXMUUEXNERZBXMXNUUDEXMUUDX
      KXTSZEXTSZEXMXLUUDUUGOUUCDXKXTVTUPXMXICXHWAZUUGUUHOYEXHACWBZUUICXTSZUUHNZ
      UUHUUGUUIUULURUUHNZUUHUUIUUKURUUHUUIYRUUKUROYSXHXTCWCWDWEUUMUUHURNUUHURUU
      HWFUUHWGWHWICEXTWJWKWLXMYDEXTWAZUUHEOYPXTAEWBXTEWMWLWNWOXMYIXJUUFBOYIXMYK
      WPYFYLXNYMRBUUFXNBUDAWQXNEYMGWRWKUOWSXNXTMUUEXQOXNFIQWTXAXNXTDXBUGXCXMXRX
      KXHSZCXHSZCXMXLXRUUOOUUCDXKXHVTUPXMXJUUOUUPOYFXJUUPEXHSZNZUUPUUOXJUURUUPU
      RNUUPXJUUQURUUPXJXTXHUQZUROZUUQUROZUUSYQURXTXHXDYSWHXJUUNUUTUVAVCXJYHUUNX
      JYHYNGYIXJYHYNVCYKYOULUKXTYGEWBUPXTXHEWCUPWDXEUUPWGWICEXHWJWKUPXMXIUUIUUP
      COYEUUJXHCWMWLXFXG $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1p1m1.1 $e |- H = { <. ( N + 1 ) , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    fseq1p1m1 $p |- ( N e. NN0 ->
        ( ( F : ( 1 ... N ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... ( N + 1 ) ) --> A /\ ( G ` ( N + 1 ) ) = B /\
            F = ( G |` ( 1 ... N ) ) ) ) ) $=
      ( cn0 wcel c1 cfz co wf cun wceq w3a caddc cfv cres wi cc0 cif cop csn
      opreq2 feq2d opreq1 opeq1d sneqd syl5eq uneq2d eqeq2d 3anbi13d opreq2d
      fveq2d eqeq1d reseq2d 3anbi123d imbi12d eqid 0nn0 elimel fseq1p1m1lem3
      dedth fseq1p1m1lem2 impbid ) FHIZJFKLZACMZBAIZDCENZOZPZJFJQLZKLZADMZVNDRZ
      BOZCDVHSZOZPZVGVMWATJVGFUAUBZKLZACMZVJDCWBJQLZBUCZUDZNZOZPZJWEKLZADMZWEDR
      ZBOZCDWCSZOZPZTFUAFWBOZVMWJWAWQWRVIWDVLWIVJWRVHWCACFWBJKUEZUFWRVKWHDWREWG
      CWRVNBUCZUDWGEWRWTWFWRVNWEBFWBJQUGZUHUIGUJUKULUMZWRVPWLVRWNVTWPWRVOWKADWR
      VNWEJKXAUNUFWRVQWMBWRVNWEDXAUOUPWRVSWOCWRVHWCDWSUQULURZUSABCDWGWBWGUTZFUA
      HVAVBZVCVDVGWAVMTWQWJTFUAWRWAWQVMWJXCXBUSABCDWGWBXDXEVEVDVF $.
      $( [17-Nov-2012] $)
  $}

  ${
    fseq1m1p1.1 $e |- H = { <. N , B >. } $.
    $( Add/remove an item to/from the end of a finite sequence.  (Contributed
       by Paul Chapman, 17-Nov-2012.) $)
    fseq1m1p1 $p |- ( N e. NN ->
        ( ( F : ( 1 ... ( N - 1 ) ) --> A /\ B e. A /\ G = ( F u. H ) ) <->
          ( G : ( 1 ... N ) --> A /\ ( G ` N ) = B /\
            F = ( G |` ( 1 ... ( N - 1 ) ) ) ) ) ) $=
      ( cn wcel c1 cmin co cfz wf caddc cop csn cun wceq w3a cfv cres cn0 wb
      nnm1nn0 eqid fseq1p1m1 syl cc nncn ax1cn npcan mpan2 opeq1d sneqd
      syl6eqr uneq2d eqeq2d 3anbi3d opreq2d feq2d fveq2d eqeq1d 3anbi12d
      3bitr3d ) FHIZJFJKLZMLZACNZBAIZDCVGJOLZBPZQZRZSZTZJVKMLZADNZVKDUAZBSZCDVH
      UBSZTZVIVJDCERZSZTJFMLZADNZFDUAZBSZWATVFVGUCIVPWBUDFUEABCDVMVGVMUFUGUHVFV
      OWDVIVJVFVNWCDVFVMECVFVMFBPZQEVFVLWIVFVKFBVFFUIIZVKFSZFUJWJJUIIWKUKFJULUM
      UHZUNUOGUPUQURUSVFVRWFVTWHWAVFVQWEADVFVKFJMWLUTVAVFVSWGBVFVKFDWLVBVCVDVE
      $.
      $( [17-Nov-2012] $)
  $}

  $( Simplify an implication between implications.  (Contributed by Paul
     Chapman, 17-Nov-2012.) $)
  imimorb $p |- ( ( ( ps -> ch ) -> ( ph -> ch ) ) <->
                  ( ph -> ( ps \/ ch ) ) ) $=
    ( wi wn wo wa pm4.61 imor orbi12i ordir pm2.1 biantru bitr4i orbi2i or12
    3bitr4ri 3bitr4i ) BCDZEZACDZFBCEZGZAEZCFZFZSUADABCFZDZTUCUAUEBCHACIJSUAIUD
    UCCFZFUDUGFUFUHUIUGUDUIUGUBCFZGUGBUBCKUJUGCLMNOUCUDCPAUGIQR $.
    $( [17-Nov-2012] $)

  ${
    imim21d.1 $e |- ( ph -> ( ps -> ch ) ) $.
    imim21d.2 $e |- ( ph -> ( ps -> ( th -> ta ) ) ) $.
    $( A deduction useful in the successor step in induction proofs.
       (Contributed by Paul Chapman, 17-Nov-2012.) $)
    imim21d $p |- ( ph -> ( ( ch -> th ) -> ( ps -> ta ) ) ) $=
      ( wi wb imim21b syl mpbird ) ACDHBEHHZBDEHHZGABCHMNIFCBDEJKL $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d B s $.  $d C s $.  $d F f g k s $.  $d G g k s $.  $d M f g k s $.
    $d N f g k s $.  $d S s $.
    $( Equality theorem for the recursive sequence builder. $)
    seqzfveqg $p |- ( ( ( S e. A /\ F e. B /\ G e. C ) /\
         ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... N ) ( F ` k ) = ( G ` k ) ) )
        ->
       ( ( <. M , S >. seq F ) ` N ) = ( ( <. M , S >. seq G ) ` N ) ) $=
      ( vs vf vg wcel w3a cuz cfv cv wceq cfz co wral wa cop cseqz wi opeq2
      opreq1d fveq1d eqeq12d imbi2d fveq1 eqeq1d ralbidv anbi2d opreq2 imbi12d
      eqeq2d visset seqzfveq vtocl2g vtoclg 3impib imp ) DAMZFBMZGCMZNIHOPMZEQZ
      FPZVHGPZRZEHISTZUAZUBZIHDUCZFUDTZPZIVOGUDTZPZRZVDVEVFVNVTUEZVEVFUBZVNIHJQ
      ZUCZFUDTZPZIWDGUDTZPZRZUEZUEWBWAUEJDAWCDRZWJWAWBWKWIVTVNWKWFVQWHVSWKIWEVP
      WKWDVOFUDWCDHUFZUGUHWKIWGVRWKWDVOGUDWLUGUHUIUJUJVGVHKQZPZVHLQZPZRZEVLUAZU
      BZIWDWMUDTZPZIWDWOUDTZPZRZUEVGVIWPRZEVLUAZUBZWFXCRZUEWJKLFGBCWMFRZWSXGXDX
      HXIWRXFVGXIWQXEEVLXIWNVIWPVHWMFUKULUMUNXIXAWFXCXIIWTWEWMFWDUDUOUHULUPWOGR
      ZXGVNXHWIXJXFVMVGXJXEVKEVLXJWPVJVIVHWOGUKUQUMUNXJXCWHWFXJIXBWGWOGWDUDUOUH
      UQUPWCEWMWOHIJURKURLURUSUTVAVBVC $.
      $( [17-Nov-2012] $)
  $}

  ${
    $d F k $.  $d K k $.  $d M k $.  $d N k $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq ` function.  (Contributed by Paul Chapman,
       17-Nov-2012.) $)
    seqzresval2g $p |- ( ( ( S e. A /\ F e. B ) /\
                           ( N e. ( ZZ>= ` M ) /\ K e. ( M ... N ) ) ) ->
                         ( ( <. M , S >. seq ( F |` ( M ... N ) ) ) ` K ) =
                         ( ( <. M , S >. seq F ) ` K ) ) $=
      ( vk wcel wa cuz cfv cfz co cop cseqz cres cvv w3a cv wceq wral
      seqzfveqg simpl simpr resexg adantl 3jca elfzuzb biimpa simpld cz wss
      elfzuz3 eluzel2 adantr fzss2 syl11anc sseld fvres eqcomd syl6 r19.21aiv
      jca syl2an ) CAIZDBIZJZGFKLZIZEFGMNZIZJZJEFCOZDPNLZEVNDVKQZPNLZVFVGVPRIZS
      EVIIZHTZDLZVTVPLZUAZHFEMNZUBZJVOVQUAVHVMABRCHDVPFEUCVHVFVGVRVFVGUDVFVGUEV
      GVRVFDVKBUFUGUHVMVSWEVMVSGEKLIZVJVLVSWFJVIEFGUIUJUKVMWCHWDVMVTWDIVTVKIZWC
      VMWDVKVTVMWFFULIZWDVKUMVIEFGUNVJWHVLFGUOUPEFGUQURUSWGWBWAVTVKDUTVAVBVCVDV
      EVA $.
      $( [17-Nov-2012] $)
  $}

  $( A restriction of its characteristic function that doesn't change the value
     of the ` seq1 ` function.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  seq1resval2g $p |- ( ( ( S e. A /\ F e. B ) /\
                          ( N e. NN /\ K e. ( 1 ... N ) ) ) ->
      ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` K ) = ( ( S seq1 F ) ` K ) ) $=
    ( wcel wa c1 cuz cfv cfz co cres cseq1 wceq cn cop cseqz seqzresval2g wb
    cvv seq1seqzg resexg sylan2 fveq1d eqeq12d adantr mpbird elnnuz biimpi
    sylanr1 ) CAGZDBGZHZFIJKGZEIFLMZGZECDUQNZOMZKZECDOMZKZPZFQGZUOUPURHZHVDEICR
    ZUSSMZKZEVGDSMZKZPZABCDEIFTUOVDVLUAVFUOVAVIVCVKUOEUTVHUMUSUBGUTVHPUNAUBCUSU
    CDUQBUDUEUFUOEVBVJABCDUCUFUGUHUIVEUPFUJUKUL $.
    $( [17-Nov-2012] $)

  $( One less than an integer greater than or equal to 2 is a positive
     integer.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  uz2m1nn $p |- ( N e. ( ZZ>= ` 2 ) -> ( N - 1 ) e. NN ) $=
    ( c2 cuz cfv wcel cz c1 clt wbr wa cmin co cn zgt1b2 wb 1z znnsub mpan
    biimpa sylbir ) ABCDEAFEZGAHIZJAGKLMEZANUAUBUCGFEUAUBUCOPGAQRST $.
    $( [17-Nov-2012] $)

  $( 1 is not in ` ( ZZ>= `` 2 ) ` .  (Contributed by Paul Chapman,
     21-Nov-2012.) $)
  1nuz2 $p |- -. 1 e. ( ZZ>= ` 2 ) $=
    ( c1 c2 cuz cfv wcel wne wn wceq eqid nne mpbir cn cz clt wbr wa zgt1b2
    zgt1b1 bitr3i simprbi mto ) ABCDEZAAFZUCGAAHAIAAJKUBALEZUCUBAMEAANOPUDUCPAQ
    ARSTUA $.
    $( [21-Nov-2012] $)

  ${
    $d ph y $.  $d ps x $.  $d x y $.
    indstr2.1 $e |- ( x = 1 -> ( ph <-> ch ) ) $.
    indstr2.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    indstr2.3 $e |- ch $.
    indstr2.4 $e |- ( x e. ( ZZ>= ` 2 ) ->
                      ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference schema).
       The first two hypotheses give us the substitution instances we need; the
       last two are the basis and the induction hypothesis.  (Contributed by
       Paul Chapman, 21-Nov-2012.) $)
    indstr2 $p |- ( x e. NN -> ph ) $=
      ( cv cn wcel c1 wceq c2 cuz cfv wo clt wbr wi wral elnn1uz2 wb wa wn cle
      nnge1 cr nnre 1re lenlt mpan syl mpbid adantl breq2 adantr mtbird
      pm2.21d r19.21aiva pm5.5 bitrd mpbiri jaoi sylbi indstr ) ABDEGDJZKLVHMNZ
      VHOPQLZREJZVHSTZBUAZEKUBZAUAZVHUCVIVOVJVIVOCHVIVOACVIVNVOAUDVIVMEKVIVKKLZ
      UEZVLBVQVLVKMSTZVPVRUFZVIVPMVKUGTZVSVKUHVPVKUILZVTVSUDZVKUJMUILWAWBUKMVKU
      LUMUNUOUPVIVLVRUDVPVHMVKSUQURUSUTVAVNAVBUNFVCVDIVEVFVG $.
      $( [21-Nov-2012] $)
  $}

  ${
    pm5.21ndd.1 $e |- ( ph -> ( ch -> ps ) ) $.
    pm5.21ndd.2 $e |- ( ph -> ( th -> ps ) ) $.
    pm5.21ndd.3 $e |- ( ph -> ( ps -> ( ch <-> th ) ) ) $.
    $( Eliminate an antecedent implied by each side of a biconditional,
       deduction version.  (Contributed by Paul Chapman, 21-Nov-2012.) $)
    pm5.21ndd $p |- ( ph -> ( ch <-> th ) ) $=
      ( wa simpl imp jca wb pm5.21nd ) ACDABHACHABACIACBEJKADHABADIADBFJKABCDLG
      JM $.
      $( [21-Nov-2012] $)
  $}

