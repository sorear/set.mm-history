$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Elementary Number Theory - The Fundamental Theorem of Arithmetic
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for ~ arith1 . $)
  arith1lemO $p |- ( F = G ->
      ( ( N = ( # ` F ) -> ( P e. ran F <-> P || ( M ` F ) ) ) <->
        ( N = ( # ` G ) -> ( P e. ran G <-> P || ( M ` G ) ) ) ) ) $=
    ( wceq chash cfv crn wcel cdivides wbr wb fveq2 eqeq2d rneq eleq2d breq2d
    bibi12d imbi12d ) BCFZEBGHZFECGHZFABIZJZABDHZKLZMACIZJZACDHZKLZMUAUBUCEBCGN
    OUAUEUIUGUKUAUDUHABCPQUAUFUJAKBCDNRST $.
    $( [21-Nov-2012] $) $( [17-Nov-2012] $)

  ${
    $d N k $.  $d j k ph $.
    arith1lemE1.1 $e |- ( ph -> G = ( F |` ( 1 ... N ) ) ) $.
    $( Lemma for ~ arith1 . $)
    arith1lemE1 $p |- ( ph -> ( A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                                ( j < k -> ( G ` j ) <_ ( G ` k ) ) <->
                                A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                                ( j < k -> ( F ` j ) <_ ( F ` k ) ) ) ) $=
      ( cv clt wbr cfv cle wi c1 cfz co wcel wa cres fveq1d breq12d fvres
      breqan12d sylan9bb imbi2d 2ralbidva ) ABHZCHZIJZUGEKZUHEKZLJZMUIUGDKZUHDK
      ZLJZMBCNFOPZUPAUGUPQZUHUPQZRZRULUOUIAULUGDUPSZKZUHUTKZLJUSUOAUJVAUKVBLAUG
      EUTGTAUHEUTGTUAUQURVAUMVBUNLUGUPDUBUHUPDUBUCUDUEUF $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d Q q $.  $d S x y $.  $d Z p q $.  $d Z x y $.  $d p y $.
    arith1lemQ1.1 $e |- S = { p e. Prime | p || Z } $.
    arith1lemQ1.2 $e |- Q = sup ( S , RR , < ) $.
    $( Lemma for ~ arith1 . $)
    arith1lemQ1 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                        ( Q e. Prime /\ Q || Z /\ ( K e. S -> K <_ Q ) ) ) $=
      ( vy vx vq c2 cuz cfv wcel cprime cdivides wbr cle wi wa cr clt csup wss
      c0 wne cv wral wrex w3a maxprmfct simprd syl5eqel breq1 crab cbvrabv
      eqtri elrab2 sylib simpld suprub sylan syl6breqr ex 3jca ) DKLMNZAONZADPQ
      ZCBNZCARQZSVFVGVHVFABNVGVHTVFBUAUBUCZBAVFBUAUDBUEUFHUGIUGRQHBUHIUAUIUJZVK
      BNZIHEBDFUKZULGUMJUGZDPQZVHJAOBVOADPUNBEUGZDPQZEOUOVPJOUOFVRVPEJOVQVODPUN
      UPUQURUSZUTVFVGVHVSULVFVIVJVFVITCVKARVLVICVKRQVFIHBCVAVFVLVMVNUTVBGVCVDVE
      $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d Q y $.  $d Z p $.  $d Z y $.
    arith1lemQ2.1 $e |- S = { p e. Prime | p || Z } $.
    arith1lemQ2.2 $e |- Q = sup ( S , RR , < ) $.
    $( Lemma for ~ arith1 . $)
    arith1lemQ2 $p |- ( Z e. ( ZZ>= ` 2 ) ->
                        ( ( Z / Q ) e. NN /\ ( Z / Q ) < Z ) ) $=
      ( cK vy c2 cuz cfv wcel cdiv co cn clt wbr cz cc0 elnnz cdivides cprime
      cle wi arith1lemQ1 simp2d cv cmul wceq wrex wb simp1d prmz syl eluzelz
      divides syl11anc wa cc mulcom cr zre recnd zcn syl2an eqeq1d rexbidva
      prmnn zdiv 3bitr2d mpbid uz2nn nngt0 divgt0 syl22anc sylanbrc c1 prmuz2
      zgt1b2 biimpri simprd 3syl ltmulgt12 syl111anc ltdivmul syl112anc mpbird
      jca ) CIJKZLZCAMNZOLZWKCPQZWLWKRLZSWKPQZWJWKTWJACUAQZWNWJAUBLZWPGBLGAUCQU
      DZABGCDEFUEZUFWJWPHUGZAUHNZCUIZHRUJZAWTUHNZCUIZHRUJZWNWJARLZCRLZWPXCUKWJW
      QXGWJWQWPWRWSULZAUMUNZICUOZHACUPUQWJXEXBHRWJWTRLZURXDXACAUSLWTUSLXDXAUIWJ
      XLAWTUTWJAWJXGAVALZXJAVBUNZVCWTVDVEVFVGWJAOLZXHXFWNUKWJWQXOXIAVHUNZXKHACV
      IUQVJVKWJCVALZSCPQZXMSAPQZWOWJXHXQXKCVBUNZWJCOLXRCVLCVMUNZXNWJXOXSXPAVMUN
      ZCAVNVOVPWJWMCACUHNPQZWJVQAPQZYCWJWQAWILZYDXIAVRYEXGYDXGYDURYEAVSVTWAWBWJ
      XQXMXRYDYCUKXTXNYACAWCWDVKWJXQXQXMXSWMYCUKXTXTXNYBCCAWEWFWGWH $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    $d G j k $.  $d N j k $.
    $( Lemma for ~ arith1 . $)
    arith1lemQ3 $p |- ( ( N e. NN0 /\ A. j e. ( 1 ... N ) A. k e. ( 1 ... N )
                                      ( j < k -> ( G ` j ) <_ ( G ` k ) ) /\
        A. j e. ( 1 ... N ) ( G ` j ) <_ ( G ` ( N + 1 ) ) ) ->
        A. j e. ( 1 ... ( N + 1 ) ) A. k e. ( 1 ... ( N + 1 ) )
        ( j < k -> ( G ` j ) <_ ( G ` k ) ) ) $=
      ( cn0 wcel cv clt wbr cfv cle wi c1 cfz co wral caddc wa wb elfzle2
      adantl cz zleltp1 elfzelz nn0z syl2an ancoms mpbid pm5.5 syl ralbidva
      adantr wn cr letrp1 zre nn0re syl3an 3com23 3anidm12 lenlt peano2re
      pm2.21d r19.21aiva ltnr jca csn cun wceq 1z a1i cn nn0p1nn nnge1 fzsuc
      syl111anc raleqdv raleqbidv cvv oprex breq1 fveq2 breq1d imbi12d breq2
      breq2d 2ralunsn ax-mp syl6bb biimpar 3impb 3expia mpdan expdimp sylbird
      3impia ) DEFZAGZBGZHIZWRCJZWSCJZKIZLZBMDNOZPAXEPZXADMQOZCJZKIZAXEPZXDBMXG
      NOZPZAXKPZWQXFRXJWRXGHIZXILZAXEPZXMWQXPXJSXFWQXOXIAXEWQWRXEFZRZXNXOXISXRW
      RDKIZXNXQXSWQWRMDTUAXQWQXSXNSZWRUBFDUBFZXTXQWQWRDUCWRMDUDDUEZUFUGUHXNXIUI
      UJUKULWQXFXPXMWQXGWSHIZXHXBKIZLZBXEPZXGXGHIZXHXHKIZLZRZXFXPRZXMLWQYFYIWQY
      EBXEWQWSXEFZRYCYDYLWQYCUMZYLWQRWSXGKIZYMYLWQYNYLWQYLYNWSUNFZDUNFZWSDKIYNY
      LWQYLWSDUOYLWSUBFYOWSMDUDWSUPUJZDUQZWSMDTURUSUTYOXGUNFZYNYMSYLWQWSXGVAYQW
      QYPYSYRDVBUJZUFUHUGVCVDWQYGYHWQYSYGUMYTXGVEUJVCVFWQYJYKXMWQYKYJXMWQYKYJXM
      WQXMYKYJRZWQXMXDBXEXGVGVHZPZAUUBPZUUAWQXLUUCAXKUUBWQMUBFZYAMXGKIZXKUUBVIU
      UEWQVJVKYBWQXGVLFUUFDVMXGVNUJMDVOVPZWQXDBXKUUBUUGVQVRXGVSFUUDUUASDMQVTXDX
      OYEYIABXEXGVSWRXGVIZWTYCXCYDWRXGWSHWAUUHXAXHXBKWRXGCWBZWCWDWSXGVIZWTXNXCX
      IWSXGWRHWEUUJXBXHXAKWSXGCWBWFWDUUHXNYGXIYHWRXGXGHWAUUHXAXHXHKUUIWCWDWGWHW
      IWJWKUSWLWMWNWOWP $.
      $( [21-Nov-2012] $) $( [17-Nov-2012] $)
  $}

  ${
    arith1lemA.1 $e |- R = { s | E. n e. NN0 ( s : ( 1 ... n ) --> Prime /\
                                 A. j e. ( 1 ... n ) A. k e. ( 1 ... n )
                                 ( j < k -> ( s ` j ) <_ ( s ` k ) ) ) } $.
    arith1lemA.2 $e |- M = { <. f , z >. | ( f e. R /\
      z = if ( f = (/) , 1 , ( ( x. seq1 f ) ` ( # ` f ) ) ) ) } $.

    ${
      $d F j k n s x $.  $d F k n s x y $.
      $( Lemma for ~ arith1 . $)
      arith1lemA $p |- ( F e. R <-> E. n e. NN0 ( F : ( 1 ... n ) --> Prime /\
        A. x e. ( 1 ... n ) A. y e. ( 1 ... n )
        ( x < y -> ( F ` x ) <_ ( F ` y ) ) ) ) $=
        ( wcel cvv c1 cv cfz co cprime wf clt wbr cfv cle wi wral wa cn0 wrex
        elisset oprex fex mpan2 adantr a1i r19.23aiv wceq feq1 fveq1 breq12d
        imbi2d 2ralbidv breq1 fveq2 breq1d imbi12d breq2 breq2d cbvral2v
        syl6bb anbi12d rexbidv elab2g pm5.21nii ) IDNIONZPHQZRSZTIUAZAQZBQZUBUC
        ZVTIUDZWAIUDZUEUCZUFZBVRUGAVRUGZUHZHUIUJZIDUKWHVPHUIWHVPUFVQUINVSVPWGVS
        VRONVPPVQRULVRTOIUMUNUOUPUQVRTKQZUAZFQZGQZUBUCZWLWJUDZWMWJUDZUEUCZUFZGV
        RUGFVRUGZUHZHUIUJWIKIDOWJIURZWTWHHUIXAWKVSWSWGVRTWJIUSXAWSWNWLIUDZWMIUD
        ZUEUCZUFZGVRUGFVRUGWGXAWRXEFGVRVRXAWQXDWNXAWOXBWPXCUEWLWJIUTWMWJIUTVAVB
        VCXEWFVTWMUBUCZWCXCUEUCZUFFGABVRVRWLVTURZWNXFXDXGWLVTWMUBVDXHXBWCXCUEWL
        VTIVEVFVGWMWAURZXFWBXGWEWMWAVTUBVHXIXCWDWCUEWMWAIVEVIVGVJVKVLVMLVNVO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F a b k n s $.  $d F a j k n s $.  $d N a b n $.
      $( Lemma for ~ arith1 . $)
      arith1lemA6 $p |- ( ( F e. R /\ N = ( # ` F ) ) <->
        ( N e. NN0 /\ F : ( 1 ... N ) --> Prime /\ A. a e. ( 1 ... N )
          A. b e. ( 1 ... N ) ( a < b -> ( F ` a ) <_ ( F ` b ) ) ) ) $=
        ( wcel chash cfv wceq wa cn0 c1 cfz co cprime wf cv clt wbr cle wi
        wral w3a eleq1 wrex arith1lemA simpl wfn fseq1hash ffn sylan2 eleq1d
        mpbird adantrr r19.23aiva sylbi syl5cbir imp wb eqtr eqcomd ancoms
        opreq2 feq2d raleqdv raleqbidv anbi12d syl biimpd ex com23 exp3a
        pm2.43d imp3a simpld simprd 3jca rcla4ev sylibr 3impb 3adant3 jca
        impbii ) GBOZIGPQZRZSZITOZUAIUBUCZUDGUEZKUFZLUFZUGUHWTGQXAGQUIUHUJZLWRU
        KZKWRUKZULZWPWQWSXDWMWOWQWOWQWNTOZWMIWNTUMWMUAFUFZUBUCZUDGUEZXBLXHUKZKX
        HUKZSZFTUNZXFKLABCDEFGHJMNUOZXLXFFTXGTOZXIXFXKXOXISZXFXOXOXIUPXPWNXGTXO
        GXHUQWNXGRZXIGXGURXHUDGUSUTZVAVBVCVDVEVFVGWPWSXDWMWOWSXDSZWMXMWOXSUJZXN
        XLXTFTXOXLXTXOXIXKXTXOXIXKXTUJZXOXIXIYAUJXPXIXKXTXPXQXLXTUJXRXQWOXLXSXQ
        WOXLXSUJXQWOSZXLXSYBXGIRZXLXSVHWOXQYCWOXQSIXGIWNXGVIVJVKYCXIWSXKXDYCXHW
        RUDGXGIUAUBVLZVMYCXJXCKXHWRYDYCXBLXHWRYDVNVOVPZVQVRVSVTVQWAVSWBWCVGVDVE
        VGZWDWPWSXDYFWEWFXEWMWOWQWSXDWMWQXSSXMWMXLXSFITYEWGXNWHWIWQWSWOXDWQWSSW
        NIWQGWRUQWNIRWSGIURWRUDGUSUTVJWJWKWL $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s x $.  $d F k n s x y $.  $d N n x y $.
      $( Lemma for ~ arith1 . $)
      arith1lemA8 $p |- ( ( F e. R /\ N = ( # ` F ) ) ->
                        ( N e. NN0 /\ F : ( 1 ... N ) --> Prime ) ) $=
        ( vx vy wcel chash cfv wceq wa cn0 c1 cfz co cprime wf cv clt wbr cle
        wi wral arith1lemA6 simp1bi simp2bi jca ) GBOIGPQRSZITOZUAIUBUCZUDGUEZU
        PUQUSMUFZNUFZUGUHUTGQVAGQUIUHUJNURUKMURUKZABCDEFGHIJMNKLULZUMUPUQUSVBVC
        UNUO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d N n $.
      $( Lemma for ~ arith1 . $)
      arith1lemA7 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                        ( F ` N ) e. Prime ) $=
        ( wcel chash cfv wceq cn cprime c1 cfz co wf wa ffvelrn cn0
        arith1lemA8 simprd cuz elnnuz eluzfz2 sylbi syl2an 3impa ) GBMZIGNOPZIQ
        MZIGORMZSITUAZRGUBZIURMZUQUNUOUCZUPURRIGUDVAIUEMUSABCDEFGHIJKLUFUGUPISU
        HOMUTIUISIUJUKULUM $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.
      $( Lemma for ~ arith1 . $)
      arith1lemA4 $p |- ( F e. R -> F e. Fin ) $=
        ( wcel c1 chash cfv cfz co cfn cn0 cprime wf wceq wa eqid arith1lemA8
        mpan2 simpld fz1fi syl cen wbr wb wfn simprd ffn cvv oprex fndmeng
        3syl enfi mpdan mpbid ) GBLZMGNOZPQZRLZGRLZVCVDSLZVFVCVHVETGUAZVCVDVDUB
        VHVIUCVDUDABCDEFGHVDIJKUEUFZUGVDUHUIVCVEGUJUKZVFVGULVCVIGVEUMZVKVCVHVIV
        JUNVETGUOVLVEUPLVKMVDPUQVEUPGURUFUSVEGBUTVAVB $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d R f z $.  $d f j k n s x $.  $d f k n s x y $.
      $( Lemma for ~ arith1 . $)
      arith1lemB $p |- M : R --> NN $=
        ( vx vy cv c0 wceq c1 chash cfv cmul cseq1 co cif cn wcel wral wf cfz
        cprime cn0 wrex clt wbr cle wi wa arith1lemA r19.40 simpld sylbi cc0
        1nn wfn wb 0fz1 ffn sylan2 biimpar iftrue syl eleq1d mpbiri wn mulex
        visset nnmulcl seq1alcl ffvelrn r19.21aiva wss prmnn ssriv fss mpan2
        fseq1hash nnnn0 syl2an fveq2d mpbird wo elnn00nn biimpi orcanai sylan
        an1rs notbid iffalse pm2.61dan r19.23aiva rgen fopab2 mpbi ) CMZNOZPXBQ
        RZSXBTUAZRZUBZUCUDZCBUEBUCGUFXHCBXBBUDZPFMZUGUAZUHXBUFZFUIUJZXHXIXLKMZL
        MZUKULXNXBRXOXBRZUMULUNLXKUEKXKUEZUOFUIUJZXMKLABCDEFXBGHIJUPXRXMXQFUIUJ
        XLXQFUIUQURUSXLXHFUIXJUIUDZXLUOZXJUTOZXHXTYAUOZXHPUCUDVAYBXGPUCYBXCXGPO
        XTXCYAXSXBXKVBZXCYAVCXLXBXJVDXKUHXBVEZVFZVGXCPXFVHVIVJVKXTYAVLZUOZXHXFU
        CUDZXSYFXLYHXJUCUDZXLYHXSYFUOYIXLUOZYHXJXERZUCUDZYIXKUCXBUFZYLXLYIXPUCU
        DZLXKUEYLYMFHLUCSXBXJVMCVNXJHMVOVPYMYNLXKXKUCXOXBVQVRVFXLUHUCVSYMFUHUCX
        JVTWAXKUHUCXBWBWCVFYJXFYKUCYJXDXJXEXSYCXDXJOYIXLXBXJWDXJWEYDWFWGVJWHXSY
        AYIXSYAYIWIXJWJWKWLWMWNYGXGXFUCYGXCVLZXGXFOXTYOYFXTXCYAYEWOVGXCPXFWPVIV
        JWHWQWRVIWSCABUCXGGJWTXA $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d R f z $.  $d j k n s x $.  $d k n s x y $.
      $( Lemma for ~ arith1 . $)
      arith1lemC0 $p |- ( (/) e. R /\ ( M ` (/) ) = 1 ) $=
        ( vx vy c0 wcel cfv c1 wceq cc0 cn0 cfz co cprime wf cv clt wbr cle wi
        wral 0nn0 f0 eqid wb fz1n ax-mp mpbir feq2i ral0 raleqi w3a chash wa
        arith1lemA6 biimpri simpld mp3an cmul cseq1 cif eqeq1 opreq2 fveq2
        fveq12d ifbieq2d cz 1z elisseti fvex ifex fvopab4 iftrue syl6eq pm3.2i
        ) MBNZMGOZPQZRSNZPRTUAZUBMUCZKUDZLUDZUEUFWJMOWKMOUGUFUHLWHUIZKWHUIZWDUJ
        WIMUBMUCUBUKWHMUBMWHMQZRRQZRULWGWNWOUMUJRUNUOUPZUQUPWMWLKMUIWLKURWLKWHM
        WPUSUPWGWIWMUTZWDRMVAOZQZWDWSVBWQABCDEFMGRHKLIJVCVDVEVFZWDWFWTWDWEMMQZP
        WRVGMVHUAZOZVIZPCAMCUDZMQZPXEVAOZVGXEVHUAZOZVIXDBGXFXFXAXIXCPXEMMVJXFXG
        WRXHXBXEMVGVHVKXEMVAVLVMVNJXAPXCPVOVPVQWRXBVRVSVTXAXDPQMULXAPXCWAUOWBUO
        WC $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f z $.  $d F j k n s $.  $d R f z $.
      $( Lemma for ~ arith1 . $)
      arith1lemD0 $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                        ( M ` F ) = ( ( x. seq1 F ) ` N ) ) $=
        ( wcel chash cfv wceq cn w3a cmul cseq1 co wa c0 c1 cif cv eqeq1
        opreq2 fveq2 fveq12d ifbieq2d 1nn elisseti fvex ifex fvopab4 adantr wn
        cc0 wne cfn wb arith1lemA4 hash0 necon3abid syl biimpa nnne0 sylan2
        iffalse eqtrd eleq1 3impb 3ad2ant2 eqtr4d ) GBMZIGNOZPZIQMZRGHOZVQSGTUA
        ZOZIWAOZVPVRVSVTWBPZVPVQQMZWDVRVSUBVPWEUBZVTGUCPZUDWBUEZWBVPVTWHPWECAGC
        UFZUCPZUDWINOZSWITUAZOZUEWHBHWIGPZWJWGWMWBUDWIGUCUGWNWKVQWLWAWIGSTUHWIG
        NUIUJUKLWGUDWBUDQULUMVQWAUNUOUPUQWFWGURZWHWBPVPVQUSUTZWOWEVPWPWOVPGVAMZ
        WPWOVBABCDEFGHJKLVCWQWGVQUSGVDVEVFVGVQVHVIWGUDWBVJVFVKVRVSWEIVQQVLVGVIV
        MVRVPWCWBPVSIVQWAUIVNVO $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s x $.  $d F k n s x y $.  $d G j k n s x $.
      $d G k n s x y $.  $d N n x y $.
      arith1lemE.3 $e |- G = ( F |` ( 1 ... N ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemE $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                       ( G e. R /\ N = ( # ` G ) ) ) $=
        ( vx vy wcel c1 caddc co chash cfv wceq cn0 w3a cfz cprime wf cv clt
        wbr cle wi wral wa simp3 cres wss fssres cz nn0z 1z fzssp1 mpan syl
        sylan2 feq1i sylibr arith1lemA6 biimpi simp2d sylan 3impa ssralv
        ralimdv syld wb id arith1lemE1 ax-mp syl6ibr simp3d syl5com 3impia
        3jca ) GBQZJRSTZGUAUBUCZJUDQZUEZWIRJUFTZUGHUHZOUIZPUIZUJUKZWMHUBWNHUBUL
        UKUMPWKUNOWKUNZUEHBQJHUAUBUCUOWJWIWLWPWFWHWIUPWFWHWIWLRWGUFTZUGGUHZWIWL
        WFWHUOZWRWIUOWKUGGWKUQZUHZWLWRWKWQURZXAWIWQUGWKGUSWIJUTQZXBJVARUTQXCXBV
        BRJVCVDVEZVFWKUGHWTNVGVHWSWGUDQZWRWOWMGUBWNGUBULUKUMZPWQUNZOWQUNZWSXEWR
        XHUEABCDEFGIWGKOPLMVIVJZVKVLVMWFWHWIWPWIXHWPWSWIXHXFPWKUNZOWKUNZWPWIXBX
        HXKUMXDXBXHXJOWQUNXKXBXGXJOWQXFPWKWQVNVOXJOWKWQVNVPVEHWTUCZWPXKVQNXLOPG
        HJXLVRVSVTWAWSXEWRXHXIWBWCWDWEABCDEFHIJKOPLMVIVH $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F g j k n s $.  $d M g $.  $d R f g z $.  $d g x y z $.
      $( Lemma for ~ arith1 . $)
      arith1lemH3 $p |- ( F e. R ->
                        ( ( # ` F ) e. NN <-> ( M ` F ) e. ( ZZ>= ` 2 ) ) ) $=
        ( vg vx vy wcel chash cfv cn c2 cuz cv wi wceq fveq2 eleq1d imbi12d wa
        cmul cseq1 co eqid arith1lemD0 mp3an2 c1 cfz wral mulex visset
        uz2mulcl seq1alcl cprime wf ffvelrn cn0 arith1lemA8 simprd mpan2 sylan
        prmuz2 syl r19.21aiva sylan2 ancoms eqeltrd ex vtoclga cc0 wo cfn
        arith1lemA4 hashcl elnn00nn sylib wn c0 wb hash0 1nuz2 arith1lemC0
        simpri syl6eq mtbiri syl6bi orim1d imor syl6ibr mpd impbid ) GBOZGPQZRO
        ZGHQZSTQZOZLUAZPQZROZXEHQZXCOZUBXAXDUBLGBXEGUCZXGXAXIXDXJXFWTRXEGPUDUEX
        JXHXBXCXEGHUDUEUFXEBOZXGXIXKXGUGXHXFUHXEUIUJQZXCXKXFXFUCZXGXHXLUCXFUKZA
        BCDEFXEHXFIJKULUMXGXKXLXCOZXGAUAZXEQZXCOZAUNXFUOUJZUPXOXKMNAXCUHXEXFUQL
        URMUANUAUSUTXKXRAXSXKXPXSOZUGXQVAOZXRXSVAXEVBZXTYAXKXSVAXPXEVCXKXMYBXNX
        KXMUGXFVDOYBABCDEFXEHXFIJKVEVFVGVHXQVIVJVKVLVMVNVOVPWSWTVQUCZXAVRZXDXAU
        BZWSWTVDOZYDWSGVSOZYFABCDEFGHIJKVTZGWAVJWTWBWCWSYDXDWDZXAVRYEWSYCYIXAWS
        YCGWEUCZYIWSYGYCYJWFYHGWGVJYJXDUNXCOWHYJXBUNXCYJXBWEHQZUNGWEHUDWEBOYKUN
        UCABCDEFHIJKWIWJWKUEWLWMWNXDXAWOWPWQWR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d R f z $.  $d f j k n s $.
      $( Lemma for ~ arith1 . $)
      arith1lemA9 $p |- ( F e. R -> ( ( # ` F ) = 0 <-> ( M ` F ) = 1 ) ) $=
        ( wcel chash cfv cn c2 cuz wa wn wo cc0 wceq c1 wb arith1lemH3 dfbi3
        sylib wi 0nnn eleq1 mtbiri con2i 1nuz2 anim12i pm5.1 syl con4bid a1i
        biorf orcom syl6bb bi2bian9 cn0 cfz co cprime wf eqid arith1lemA8
        mpan2 simpld elnn00nn arith1lemB ffvelrn mpan elnn1uz2 2thd syl5bir
        com12 jaod mpd ) GBLZGMNZOLZGHNZPQNZLZRZWDSZWGSZRZTZWCUAUBZWEUCUBZUDZWB
        WDWGUDWLABCDEFGHIJKUEWDWGUFUGWBWHWOWKWHWOUHWBWHWMWNWHWMSZWNSZRWPWQUDWDW
        PWGWQWMWDWMWDUAOLUIWCUAOUJUKULWNWGWNWGUCWFLUMWEUCWFUJUKULUNWPWQUOUPUQUR
        WKWBWOWKWOWMWDTZWNWGTZUDWBWIWMWRWJWNWSWIWMWDWMTWRWDWMUSWDWMUTVAWJWNWGWN
        TWSWGWNUSWGWNUTVAVBWBWRWSWBWCVCLZWRWBWTUCWCVDVEVFGVGZWBWCWCUBWTXARWCVHA
        BCDEFGHWCIJKVIVJVKWCVLUGWBWEOLZWSBOHVGWBXBABCDEFHIJKVMBOGHVNVOWEVPUGVQV
        RVSVTWA $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s x $.  $d F k n s x y $.  $d F x z $.  $d N n x y $.
      $d N x z $.  $d P z $.  $d R x $.
      $( Lemma for ~ arith1 . $)
      arith1lemG $p |- ( ( F e. R /\ N = ( # ` F ) /\ N e. NN ) ->
                        ( P e. ran F -> P <_ ( F ` N ) ) ) $=
        ( vx vy crn wcel cv cfv cle wbr wral chash wceq cn w3a breq1 rcla4v c1
        cfz co clt wi cn0 cprime wf simp1 simp2 wa arith1lemA6 biimpi syl11anc
        simp3d wo elfzle2 adantl wb cr leloe cz elfzelz zre syl simp3 cuz
        elnnuz eluzfz2 sylbi 3syl syl2an ancoms mpbid adantr breq2 fveq2
        breq2d imbi12d imp eqle ffvelrn prmz simp2d sylan ex jaod mpd exp31
        a2d ralimdv2 wfn ffn wfo dffn4 cbvfo syl5com ) BHPZQARZJHSZTUAZAXFUBZBX
        HTUAZHCQZJHUCSUDZJUEQZUFZXIXKABXFXGBXHTUGUHXONRZHSZXHTUAZNUIJUJUKZUBZXJ
        XOXPORZULUAZXQYAHSZTUAZUMZOXSUBZNXSUBZXTXOJUNQZXSUOHUPZYGXOXLXMYHYIYGUF
        ZXLXMXNUQXLXMXNURXLXMUSYJACDEFGHIJKNOLMUTVAVBZVCXOYFXRNXSXSXOXPXSQZYFXR
        XOYLYFXRXOYLUSZYFUSZXPJULUAZXPJUDZVDZXRYMYQYFYMXPJTUAZYQYLYRXOXPUIJVEVF
        YLXOYRYQVGZXPVHQZJVHQZYSYLXOXPJVIYLXPVJQYTXPUIJVKXPVLVMXOJXSQZJVJQUUAXO
        XNUUBXLXMXNVNXNJUIVOSQUUBJVPUIJVQVRVMZJUIJVKJVLVSVTWAWBWCYNYOXRYPYMYFYO
        XRUMZXOYFUUDUMZYLXOUUBUUEUUCYEUUDOJXSYAJUDZYBYOYDXRYAJXPULWDUUFYCXHXQTY
        AJHWEWFWGUHVMWCWHYMYPXRUMYFYMYPXRXQVHQZXQXHUDXRYMYPXQXHWIYIYLUUGXOYIYLU
        SXQUOQXQVJQUUGXSUOXPHWJXQWKXQVLVSXOYHYIYGYKWLZWMXPJHWEVTWNWCWOWPWQWRWSW
        PXOYIHXSWTZXTXJVGZUUHXSUOHXAUUIXSXFHXBUUJXSHXCXRXINAXSXFHXQXGXHTUGXDVRV
        SWBXE $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d R f z $.  $d Z p $.  $d f j k n s $.
      $( Lemma for ~ arith1 . $)
      arith1lemK2 $p |- ( ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ Q e. NN ) ->
                      ( ( Z / Q ) = ( M ` F ) <-> Z = ( ( M ` F ) x. Q ) ) ) $=
        ( wcel c2 cuz cfv cn w3a cc cc0 wne wa cdiv co wceq cmul wb cz simp2
        eluzelz zcn 3syl wf arith1lemB a1i simp1 ffvelrn syl11anc nncn syl
        simp3 nnne0 jca divmul3 syl111anc ) HCNZJOPQNZBRNZSZJTNZHIQZTNZBTNZBUAU
        BZUCZJBUDUEVLUFJVLBUGUEUFUHVJVHJUINVKVGVHVIUJOJUKJULUMVJVLRNZVMVJCRIUNZ
        VGVQVRVJACDEFGIKLMUOUPVGVHVIUQCRHIURUSVLUTVAVJVIVPVGVHVIVBVIVNVOBUTBVCV
        DVAJVLBVEVF $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f z $.  $d F j k n s $.  $d G f z $.  $d G j k n s $.  $d N n $.
      $d R f z $.
      arith1lemL2.3 $e |- G = ( F |` ( 1 ... N ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemL2 $p |- ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) ->
                        ( M ` F ) = ( ( M ` G ) x. ( F ` ( N + 1 ) ) ) ) $=
        ( wcel c1 caddc co chash cfv wceq cn0 cmul wa cc0 cn wo w3a cc cprime
        cz arith1lemA7 prmz zcn 3syl 0nn0 eleq1 mpbiri nn0p1nn syl syl3an3
        mulid2 c0 arith1lemE simprd wb eqeq1 3ad2ant3 mpbid eqcomd cfn simpld
        arith1lemA4 hash0 fveq2d arith1lemC0 simpri syl6eq opreq1d cseq1
        arith1lemD0 opreq1 ax1cn addid2i cvv mulex seq11g mpan 3ad2ant1 3eqtrd
        eqtr4d 3eqtr4rd 3expia seq1p1g cfz cres seq1resval2g cuz elnnuz
        eluzfz2 sylbi ancli sylan2 3impa opreq2i fveq1i syl5eq mp3an1 3adant2
        peano2nn nnnn0 simp3 syl111anc 3eqtr4d jaod elnn00nn syl5ib 3impia ) GB
        OZJPQRZGSTUAZJUBOZGITZHITZXTGTZUCRZUAZXSYAUDZJUEUAZJUFOZUGYGYBYHYIYGYJX
        SYAYIYGXSYAYIUHZPYEUCRZYEYFYCYKYEUIOZYLYEUAXSYAXTUFOZYMYIXSYAYNUHYEUJOY
        EUKOYMABCDEFGIXTKLMULYEUMYEUNUOYIYBYNYIYBUEUBOUPJUEUBUQURZJUSUTZVAYEVBU
        TYKYDPYEUCYKYDVCITZPYKHVCIYKHSTZUEUAZHVCUAZYKUEYRYKJYRUAZUEYRUAZYKHBOZU
        UAXSYAYBUUCUUAUDZYIABCDEFGHIJKLMNVDZYOVAZVEYIXSUUAUUBVFYAJUEYRVGVHVIVJY
        KUUCHVKOYSYTVFYKUUCUUAUUFVLABCDEFHIKLMVMHVNUOVIVOVCBOYQPUAABCDEFIKLMVPV
        QVRVSYKYCPGTZYEYKYCXTUCGVTRZTZPUUHTZUUGXSYAYNYCUUIUAZYIABCDEFGIXTKLMWAZ
        YPVAYKXTPUUHYIXSXTPUAYAYIXTUEPQRPJUEPQWBPWCWDVRVHZVOXSYAUUJUUGUAZYIUCWE
        OZXSUUNWFWEBUCGWGWHWIWJYKXTPGUUMVOWKWLWMXSYAYJYGXSYAYJUHZUUIJUCHVTRZTZY
        EUCRZYCYFXSYJUUIUUSUAZYAUUOXSYJUUTWFUUOXSYJUHZUUIJUUHTZYEUCRUUSJWEBUCGW
        NUVAUURUVBYEUCUVAJUCGPJWORZWPZVTRZTZUVBUURUUOXSYJUVFUVBUAZUUOXSUDYJJUVC
        OZUDUVGYJWEBUCGJJWQYJUVHYJJPWRTOUVHJWSPJWTXAXBXCXDJUUQUVEHUVDUCVTNXEXFX
        GVSWKXHXIXSYAYNUUKYJUULJXJVAUUPYDUURYEUCUUPUUCUUAYJYDUURUAUUPUUCUUAXSYA
        YBUUDYJUUEJXKVAZVLUUPUUCUUAUVIVEXSYAYJXLABCDEFHIJKLMWAXMVSXNWMXOJXPXQXR
        $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f j k n s $.  $d F f z $.  $d G f j k n s $.  $d G f z $.  $d N n $.
      $d R f z $.
      arith1lemM.3 $e |- G = ( F |` ( 1 ... N ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemM $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) /\
                         P e. Prime ) ->
        ( P || ( M ` F ) <-> ( P || ( M ` G ) \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
        ( cprime wcel c1 caddc co chash cfv wceq cn0 w3a cdivides wbr wo wb wa
        cmul arith1lemL2 breq2d adantl cz euclemmaNEW 3expb cn arith1lemE
        simpld wf arith1lemB ffvelrn mpan nnz 3syl arith1lemA7 nn0p1nn syl3an3
        prmz syl jca sylan2 c2 cuz dvdsprm prmuz2 syl2an orbi2d 3bitrd ancoms
        ) BPQZHCQZKRSTZHUAUBUCZKUDQZUEZBHJUBZUFUGZBIJUBZUFUGZBWDHUBZUCZUHZUIWBW
        GUJZWIBWJWLUKTZUFUGZWKBWLUFUGZUHZWNWGWIWQUIWBWGWHWPBUFACDEFGHIJKLMNOULU
        MUNWBWJUOQZWLUOQZUJWQWSUIZWGWBWTXAXBBWJWLUPUQWGWTXAWGICQZWJURQZWTWGXCKI
        UAUBUCACDEFGHIJKLMNOUSUTCURJVAXCXDACDEFGJLMNVBCURIJVCVDWJVEVFWGWLPQZXAW
        CWEWDURQXEWFACDEFGHJWDLMNVGKVHVIZWLVJVKVLVMWOWRWMWKBVNVOUBQXEWRWMUIWBWG
        WLBVPBVQXFVRVSVTWA $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d N n $.
      arith1lemN.3 $e |- G = ( F |` ( 1 ... N ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemN $p |- ( ( ( F e. R /\ ( N + 1 ) = ( # ` F ) /\ N e. NN0 ) /\
                         P e. Prime ) ->
        ( P e. ran F <-> ( P e. ran G \/ P = ( F ` ( N + 1 ) ) ) ) ) $=
        ( wcel c1 caddc co chash cfv wceq cn0 w3a crn csn cun cprime wo wb cfz
        wfn wa cima imaundi fnima cz cle wbr nn0z cn nn0p1nn nnge1 syl 1z
        fzsuc mp3an1 syl11anc imaeq2d sylan9req cres rneqi df-ima eqtr4i a1i
        fnsnfv cuz elnnuz eluzfz2 sylbi sylan2 uneq12d eqeq12d mpbiri eleq2d
        wf arith1lemA8 simprd ffn sylan 3impa elsncg orbi2d elun syl5bb
        sylan9bb ) HCPZKQRSZHTUAUBZKUCPZUDBHUEZPZBIUEZWRHUAZUFZUGZPZBUHPZBXCPZB
        XDUBZUIZWQWSWTXBXGUJZHQWRUKSZULZWTXLWQWSUMZXNWTUMZXAXFBXPXAXFUBHQKUKSZW
        RUFZUGZUNZHXQUNZHXRUNZUGZUBHXQXRUOXPXAXTXFYCXNWTXAHXMUNXTXMHUPWTXMXSHWT
        KUQPZQWRURUSZXMXSUBZKUTWTWRVAPZYEKVBZWRVCVDQUQPYDYEYFVEQKVFVGVHVIVJXPXC
        YAXEYBXCYAUBXPXCHXQVKZUEYAIYIOVLHXQVMVNVOXNWRXMPZXEYBUBWTXMWRHVPWTYGYJY
        HYGWRQVQUAPYJWRVRQWRVSVTVDWAWBWCWDWEXOXMUHHWFZXNXOWRUCPYKACDEFGHJWRLMNW
        GWHXMUHHWIVDWJWKXHXIBXEPZUIXKXGXHYLXJXIBXDUHWLWMBXCXEWNWOWP $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F g j k n s $.  $d F g m $.  $d M g h y $.  $d M g m x $.
      $d P g h y $.  $d P g m x $.  $d R f g y z $.  $d R g h y $.
      $d R g m x $.  $d f g j k n s y $.  $d x y $.
      arith1lemO1.3 $e |- P e. Prime $.
      $( Lemma for ~ arith1 . $)
      arith1lemO1 $p |- ( F e. R -> ( P e. ran F <-> P || ( M ` F ) ) ) $=
        ( vm vg vx vy vh chash cfv cn0 wcel crn cdivides wbr wb wceq eqid wa
        c1 cfz co cprime wf arith1lemA8 simpld mpan2 wi fvex cv eleq1 wral
        arith1lemO rcla4v cc0 caddc eqeq1 imbi1d ralbidv c0 noel wn nprmdvds1
        ax-mp 2false cfn arith1lemA4 hash0 syl biimpa rneqd rn0 syl6eq eleq2d
        fveq2d arith1lemC0 simpri breq2d bibi12d mpbiri eqcom sylan2br ex rgen
        w3a cres com12 imp3a arith1lemE syl5 wo arith1lemN arith1lemM orbi1
        syl5bir sylcom 3expia com3l imp exp3a r19.21aiv cbvralv syl5ib nn0ind
        com13 sylbird vtocle mpcom ) HSTZUAUBZHCUBZBHUCUBBHITUDUEUFZYAXSXSUGZXT
        XSUHYAYCUIXTUJXSUKULUMHUNACDEFGHIXSJKLUOUPUQXTYAYBURZURNXSHSUSNUTZXSUGZ
        XTYEUAUBZYDYEXSUAVAYAYGYFYBYAYEOUTZSTZUGZBYHUCZUBZBYHITZUDUEZUFZURZOCVB
        ZYFYBURZYGYPYROHCBYHHIYEVCVDPUTZYIUGZYOURZOCVBVEYIUGZYOURZOCVBQUTZYIUGZ
        YOURZOCVBZUUDUJVFULZYIUGZYOURZOCVBZYQPQYEYSVEUGZUUAUUCOCUULYTUUBYOYSVEY
        IVGVHVIYSUUDUGZUUAUUFOCUUMYTUUEYOYSUUDYIVGVHVIYSUUHUGZUUAUUJOCUUNYTUUIY
        OYSUUHYIVGVHVIYSYEUGZUUAYPOCUUOYTYJYOYSYEYIVGVHVIUUCOCYHCUBZUUBYOUUPYIV
        EUGZYOUUBUUPUUQUIZYOBVJUBZBUJUDUEZUFUUSUUTBVKBUMUBZUUTVLMBVMVNVOUURYLUU
        SYNUUTUURYKVJBUURYKVJUCVJUURYHVJUUPUUQYHVJUGZUUPYHVPUBUUQUVBUFACDEFGYHI
        JKLVQYHVRVSVTZWAWBWCWDUURYMUJBUDUURYMVJITZUJUURYHVJIUVCWEVJCUBUVDUJUGAC
        DEFGIJKLWFWGWCWHWIWJYIVEWKWLWMWNUUDUAUBZUUDRUTZSTUGBUVFUCUBBUVFITUDUEUF
        URZRCVBZUUKUUGUVEUVHUUKUVEUVHUIZUUJOCUVIUUPUUIYOUVEUVHUUPUUIUIZYOURUVJU
        VEUVHYOUUPUUIUVEUVHYOURUVHUUPUUIUVEWOZYOUVHUVKBYHUJUUDUKULWPZUCUBZBUVLI
        TUDUEZUFZYOUVHUVLCUBZUUDUVLSTUGZUIUVOUVKUVHUVPUVQUVOUVPUVHUVQUVOURZUVGU
        VRRUVLCBUVFUVLIUUDVCVDWQWRACDEFGYHUVLIUUDJKLUVLUHZWSWTUVKUVAUVOYOURMUVK
        UVAUIZYOUVMBUUHYHTUGZXAZUVNUWAXAZUFUVOUVTYLUWBYNUWCABCDEFGYHUVLIUUDJKLU
        VSXBABCDEFGYHUVLIUUDJKLUVSXCWIUVMUVNUWAXDXEUQXFWQXGXHXIXJXKWMUUFUVGORCB
        YHUVFIUUDVCXLXMXNWTXOXPXQXR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d R f z $.  $d f j k n s $.
      $( Lemma for ~ arith1 . $)
      arith1lemO5 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ P e. Prime ) ->
                        ( P e. ran F <-> P || Z ) ) $=
        ( wcel cfv wceq cprime w3a crn cdivides wbr wb wi c2 cif eleq1 breq1
        bibi12d imbi2d 2prm elimel arith1lemO1 dedth impcom 3adant2 breq2
        3ad2ant2 bitr4d ) HCNZJHIOZPZBQNZRBHSZNZBUTTUAZBJTUAZUSVBVDVEUBZVAVBUSV
        GVBUSVGUCUSVBBUDUEZVCNZVHUTTUAZUBZUCBUDBVHPZVGVKUSVLVDVIVEVJBVHVCUFBVHU
        TTUGUHUIAVHCDEFGHIKLMBUDQUJUKULUMUNUOVAUSVFVEUBVBJUTBTUPUQUR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d F q $.  $d F z $.  $d Q q $.  $d Q z $.  $d R f z $.
      $d S x y $.  $d Z p q $.  $d Z x y $.  $d f j k n s $.  $d p y $.
      arith1lemP2.3 $e |- S = { p e. Prime | p || Z } $.
      arith1lemP2.4 $e |- Q = sup ( S , RR , < ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemP2 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                        ( F ` ( # ` F ) ) = Q ) $=
        ( vq vy vx wcel cfv wceq c2 cuz w3a chash cle wbr wa cprime cdivides
        cv breq1 crab cbvrabv eqtri elrab2 cn simp1 eqidd simp3 simp2 eleq1d
        mpbid arith1lemH3 biimpar syl11anc arith1lemA7 syl111anc crn c1 cfz co
        wf cn0 arith1lemA8 simprd wfn fnfvelrn ffn elnnuz eluzfz2 sylbi syl2an
        wb arith1lemO5 sylanbrc cr clt csup wss c0 wne wral wrex suprub
        maxprmfct syl simpld sylan syl6breqr mpdan syl5eqel wi biimprd 3expia
        imp3a syl5ib arith1lemG syld mpd jca cz prmz zre 3syl arith1lemQ1
        simp1d letri3 mpbird ) ICUAZKIJUBZUCZKUDUEUBZUAZUFZIUGUBZIUBZBUCZYIBUHU
        IZBYIUHUIZUJZYGYKYLYGYIDUAZYKYNYIUKUAZYIKULUIZYGRUMZKULUIZYPRYIUKDYQYIK
        ULUNDMUMZKULUIZMUKUOYRRUKUOPYTYRMRUKYSYQKULUNUPUQZURYGYBYHYHUCZYHUSUAZY
        OYBYDYFUTZYGYHVAZYGYBYCYEUAZUUCUUDYGYFUUFYBYDYFVBZYGKYCYEYBYDYFVCZVDVEY
        BUUCUUFACEFGHIJLNOVFVGVHZACEFGHIJYHLNOVIVJZYGYIIVKZUAZYPYGVLYHVMVNZUKIV
        OZUUCUULYGYBUUBUUNUUDUUEYBUUBUJYHVPUAUUNACEFGHIJYHLNOVQVRVHUUIIUUMVSYHU
        UMUAZUULUUNUUCUUMYHIVTUUMUKIWAUUCYHVLUEUBUAUUOYHWBVLYHWCWDWEVHYGYBYDYOU
        ULYPWFUUDUUHUUJAYICEFGHIJKLNOWGVJVEWHYGYNUJYIDWIWJWKZBUHDWIWLDWMWNSUMTU
        MUHUISDWOTWIWPUFZYNYIUUPUHUIYGTSDYIWQYGUUQUUPDUAZYGYFUUQUURUJUUGTSMDKPW
        RWSZWTXAQXBXCYGBDUAZYLYGUUPDBYGUUQUURUUSVRQXDYGUUTBUUKUAZYLYGBUKUAZBKUL
        UIZUJUVAUUTYGUVBUVCUVAYGYBYDUVBUVCUVAXEZXEUUDUUHYBYDUVBUVDYBYDUVBUFUVAU
        VCABCEFGHIJKLNOWGXFXGVHXHYRUVCRBUKDYQBKULUNUUAURXIYGYBUUBUUCUVAYLXEUUDU
        UEUUIABCEFGHIJYHLNOXJVJXKXLXMYGYIWIUAZBWIUAZYJYMWFYGYOYIXNUAUVEUUJYIXOY
        IXPXQYGUVBBXNUAUVFYGUVBUVCYNYKXEZYGYFUVBUVCUVGUFUUGBDYIKMPQXRWSXSBXOBXP
        XQYIBXTVHYA $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s x $.  $d F p x $.  $d F k n s x y $.  $d G f j k n s $.
      $d G j k n s x $.  $d G k n s x y $.  $d G f z $.  $d N f j k n s $.
      $d N j k n s x $.  $d N k n s x y $.  $d N f z $.  $d R f z $.  $d Z p $.
      $d ph x y $.
      arith1lemQ4.3 $e |- S = { p e. Prime | p || Z } $.
      arith1lemQ4.4 $e |- Q = sup ( S , RR , < ) $.
      arith1lemQ4.5 $e |- G = ( F u. { <. ( N + 1 ) , Q >. } ) $.
      arith1lemQ4.6 $e |- N = ( # ` F ) $.
      arith1lemQ4.7 $e |- ( ph <->
      ( F e. R /\ Z e. ( ZZ>= ` 2 ) /\ ( Z / Q ) = ( M ` F ) ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemQ4 $p |- ( ph -> ( G e. R /\ Z = ( M ` G ) ) ) $=
        ( vx vy wcel cfv wceq c1 caddc co chash cn0 cfz cprime wf cv clt wbr
        cle wi wral wa w3a c2 cuz cdiv simp1bi a1i arith1lemA6 biimpi syl11anc
        simp1d peano2nn0 syl cres simp2d cdivides simp2bi arith1lemQ1 cop csn
        cun eqid fseq1p1m1 biimpa mp3anr3 syl12anc simp3d arith1lemE1 mpbid
        breq1 elrab2 ffvelrn sylan cmul crn wfn fnfvelrn ffn wb adantr eqidd
        arith1lemO5 syl111anc cz prmz cn arith1lemB mpan nnz 3syl dvdsmultr1
        mpd prmnn simp3bi arith1lemK2 syl31anc breq2d mpbird sylanbrc fveq1d
        fvres sylan9eq eqcomd breq12d r19.21aiva arith1lemQ3 biimpri simpld
        fveq2d opreq12d simprd arith1lemL2 3eqtr4d jca ) AKDUFZNKLUGZUHAYQMUIUJ
        UKZKULUGUHZAYSUMUFZUIYSUNUKZUOKUPZUDUQZUEUQZURUSZUUDKUGZUUEKUGUTUSVAZUE
        UUBVBUDUUBVBZYQYTVCZAMUMUFZUUAAUUKUIMUNUKZUOJUPZUUFUUDJUGZUUEJUGUTUSVAU
        EUULVBUDUULVBZAJDUFZMJULUGUHZUUKUUMUUOVDZAUUPNVEVFUGUFZNCVGUKJLUGZUHZUC
        VHZUUQAUBVIUUPUUQVCUURBDFGHIJLMOUDUEQRVJVKVLZVMZMVNVOAUUCYSKUGZCUHZJKUU
        LVPZUHZAUUKUUMCUOUFZUUCUVFUVHVDZUVDAUUKUUMUUOUVCVQZAUVICNVRUSZUUNEUFZUU
        NCUTUSZVAZAUUSUVIUVLUVOVDAUUPUUSUVAUCVSZCEUUNNPSTVTVOZVMZUUKUUMUVIKJYSC
        WAWBZWCUHZUVJUAUUKUUMUVIUVTVDUVJUOCJKUVSMUVSWDWEWFWGWHZVMAUUKUUHUEUULVB
        UDUULVBZUUGUVEUTUSZUDUULVBUUIUVDAUUOUWBAUUKUUMUUOUVCWIAUDUEKJMAUUCUVFUV
        HUWAWIZWJWKAUWCUDUULAUUDUULUFZVCZUVNUWCUWFUVMUVNUVMUUNUOUFZUUNNVRUSZUWF
        PUQZNVRUSUWHPUUNUOEUWIUUNNVRWLSWMUUMUWEUWGAUULUOUUDJWNUVKWOZUWFUWHUUNUU
        TCWPUKZVRUSZUWFUUNUUTVRUSZUWLUWFUUNJWQUFZUWMJUULWRZUWEUWNAUULUUDJWSAUUM
        UWOUVKUULUOJWTVOWOUWFUUPUUTUUTUHUWGUWNUWMXAAUUPUWEUVBXBUWFUUTXCUWJBUUND
        FGHIJLUUTOQRXDXEWKUWFUUNXFUFZUUTXFUFZCXFUFZUWMUWLVAUWFUWGUWPUWJUUNXGVOA
        UWQUWEAUUPUUTXHUFZUWQUVBDXHLUPUUPUWSBDFGHILOQRXIDXHJLWNXJUUTXKXLXBAUWRU
        WEAUVIUWRUVRCXGVOXBUUNUUTCXMXEXNAUWHUWLXAUWEANUWKUUNVRAUUPUUSCXHUFZUVAN
        UWKUHZUVBUVPAUVIUWTUVRCXOVOAUUPUUSUVAUCXPUUPUUSUWTVDUVAUXABCDFGHIJLNOQR
        XQWFXRZXSXBXTYAAUVOUWEAUVIUVLUVOUVQWIXBXNUWFUUNUUGCUVEUTAUWEUUNUUDUVGUG
        UUGAUUDJUVGUWDYBUUDUULKYCYDACUVEUHUWEAUVECAUUCUVFUVHUWAVQYEZXBYFWKYGUDU
        EKMYHXEUUJUUAUUCUUIVDBDFGHIKLYSOUDUEQRVJYIXEZYJZAUWKUVGLUGZUVEWPUKZNYRA
        UUTUXFCUVEWPAJUVGLUWDYKUXCYLUXBAYQYTUUKYRUXGUHUXEAYQYTUXDYMUVDBDFGHIKUV
        GLMOQRUVGWDYNXEYOYP $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d G f j k n s $.  $d G g $.  $d G f z $.  $d M g h y $.
      $d N f j k n s $.  $d N f z $.  $d Q h y $.  $d R f z $.  $d R g h y $.
      $d g h x y $.  $d h j k n s $.  $d h p x $.
      arith1lemQ5.3 $e |- S = { p e. Prime | p || x } $.
      arith1lemQ5.4 $e |- Q = sup ( S , RR , < ) $.
      arith1lemQ5.5 $e |- G = ( h u. { <. ( N + 1 ) , Q >. } ) $.
      arith1lemQ5.6 $e |- N = ( # ` h ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemQ5 $p |- ( x e. ( ZZ>= ` 2 ) -> ( A. y e. NN
        ( y < x -> E. g e. R y = ( M ` g ) ) -> E. g e. R x = ( M ` g ) ) ) $=
        ( cv c2 cuz cfv wcel clt wbr wceq wrex wi cn wral cdiv co arith1lemQ2
        simprd simpld breq1 eqeq1 rexbidv fveq2 eqeq2d cbvrexv syl5bb imbi12d
        rcla4v syl mpid w3a wa biid arith1lemQ4 rcla4ev 3expia ancoms
        r19.23adva syld ) AUDZUEUFUGUHZBUDZWAUIUJZWCHUDZNUGZUKZHEULZUMZBUNUOZWA
        DUPUQZIUDZNUGZUKZIEULZWAWFUKZHEULZWBWJWKWAUIUJZWOWBWKUNUHZWRDFWAQTUAURZ
        USWBWSWJWRWOUMZUMWBWSWRWTUTWIXABWKUNWCWKUKZWDWRWHWOWCWKWAUIVAXBWCWMUKZI
        EULWOWHXBXCWNIEWCWKWMVBVCWGXCHIEWEWLUKWFWMWCWEWLNVDVEVFVGVHVIVJVKWBWNWQ
        IEWLEUHZWBWNWQUMXDWBWNWQXDWBWNVLZMEUHWAMNUGZUKZVMWQXECDEFGJKLWLMNOWAPQR
        STUAUBUCXEVNVOWPXGHMEWEMUKWFXFWAWEMNVDVEVPVJVQVRVSVT $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d M g h x y $.  $d R f h x z $.  $d R g h x y $.  $d Z g x $.
      $d f h j k n p s x $.  $d g h p x y $.  $d p x z $.
      $( Lemma for ~ arith1 . $)
      arith1lemQ9 $p |- ( Z e. NN -> E. g e. R Z = ( M ` g ) ) $=
        ( vx vy vp vh cv cfv wceq wrex cn eqeq1 rexbidv c1 c0 wcel wa
        arith1lemC0 eqcom anbi2i mpbi fveq2 eqeq2d rcla4ev ax-mp cdivides wbr
        cprime crab cr clt csup chash caddc co cop csn cun eqid arith1lemQ5
        indstr2 vtoclga ) MQZDQZHRZSZDBTZIVOSZDBTMIUAVMISVPVRDBVMIVOUBUCVQNQZVO
        SZDBTUDVOSZDBTZMNVMUDSVPWADBVMUDVOUBUCVMVSSVPVTDBVMVSVOUBUCUEBUFZUDUEHR
        ZSZUGZWBWCWDUDSZUGWFABCEFGHJKLUHWGWEWCWDUDUIUJUKWAWEDUEBVNUESVOWDUDVNUE
        HULUMUNUOMNAOQVMUPUQOURUSZUTVAVBZBWHCDPEFGPQZWJVCRZUDVDVEWIVFVGVHZHWKJO
        KLWHVIWIVIWLVIWKVIVJVKVL $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F j k n s $.  $d N n $.
      arith1lemR3.3 $e |- N = ( # ` F ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemR3 $p |- ( F e. R ->
        dom ( F |` ( 1 ... ( N - 1 ) ) ) = ( 1 ... ( N - 1 ) ) ) $=
        ( wcel c1 cmin co cfz cdm wss cres wceq cn0 cprime wf chash cfv wa
        arith1lemA8 mpan2 simpld caddc cz nn0z peano2zm 1z fzssp1 mpan 3syl cc
        nn0cn ax1cn npcan syl opreq2d sseqtrd simprd fdm sseqtr4d ssdmres
        sylib ) GBNZOIOPQZRQZGSZTGVNUASVNUBVLVNOIRQZVOVLIUCNZVNVPTVLVQVPUDGUEZV
        LIGUFUGUBVQVRUHMABCDEFGHIJKLUIUJZUKVQVNOVMOULQZRQZVPVQIUMNVMUMNZVNWATZI
        UNIUOOUMNWBWCUPOVMUQURUSVQVTIORVQIUTNZVTIUBZIVAWDOUTNWEVBIOVCUJVDVEVFVD
        VLVRVOVPUBVLVQVRVSVGVPUDGVHVDVIVNGVJVK $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d Q z $.  $d R f z $.  $d Z p $.  $d f j k n s $.  $d g j k n s $.
      $d g z $.  $d h j k n s $.  $d h z $.
      arith1lemR4.3 $e |- S = { p e. Prime | p || Z } $.
      arith1lemR4.4 $e |- Q = sup ( S , RR , < ) $.
      arith1lemR4.5 $e |- G = ( g |` ( 1 ... ( ( # ` g ) - 1 ) ) ) $.
      arith1lemR4.6 $e |- H = ( h |` ( 1 ... ( ( # ` h ) - 1 ) ) ) $.
      arith1lemR4.7 $e |- ( ph <-> ( Z e. ( ZZ>= ` 2 ) /\ ( g e. R /\ h e. R )
          /\
                                   ( Z = ( M ` g ) /\ Z = ( M ` h ) ) ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemR4 $p |- ( ( ph /\ G = H ) -> g = h ) $=
        ( wceq wa cv chash cfv cop csn cun simpr c1 cmin co cfz cdm dmeq
        adantl wb wcel c2 cuz simp2bi simpld simprd cres eqid arith1lemR3
        dmeqi syl5eq eqeqan12d syl11anc adantr mpbid cn0 cn simp3bi simp1bi
        eqeltrrd arith1lemH3 biimpar nnm1nn0 syl fz1eqb cc nncn ax1cn a1i
        subcan2 syl111anc opeq1d sneqd uneq12d cprime wf w3a eqidd arith1lemA8
        arith1lemP2 fseq1m1p1 syl13anc simp3d eqeq12d mpbird ) ALMUEZUFZGUGZHUG
        ZUEZLXIUHUIZCUJZUKZULZMXJUHUIZCUJZUKZULZUEZXHLMXNXRAXGUMXHXMXQXHXLXPCXH
        XLUNUOUPZXPUNUOUPZUEZXLXPUEZXHUNYAUQUPZUNYBUQUPZUEZYCXHLURZMURZUEZYGXGY
        JALMUSUTAYJYGVAZXGAXIDVBZXJDVBZYKAYLYMAOVCVDUIZVBZYLYMUFZOXINUIZUEZOXJN
        UIZUEZUFZUDVEZVFZAYLYMUUBVGZYLYMYHYEYIYFYLXIYEVHZURYEYHBDFIJKXINXLPRSXL
        VIVJLUUEUBVKVLYMXJYFVHZURYFYIBDFIJKXJNXPPRSXPVIVJMUUFUCVKVLVMVNVOVPAYGY
        CVAZXGAYAVQVBZYBVQVBZUUGAXLVRVBZUUHAYLYQYNVBZUUJUUCAOYQYNAYRYTAYOYPUUAU
        DVSZVFZAYOYPUUAUDVTZWAYLUUJUUKBDFIJKXINPRSWBWCVNZXLWDWEAXPVRVBZUUIAYMYS
        YNVBZUUPUUDAOYSYNAYRYTUULVGZUUNWAYMUUPUUQBDFIJKXJNPRSWBWCVNZXPWDWEYAYBW
        FVNVOVPAYCYDVAZXGAXLWGVBZXPWGVBZUNWGVBZUUTAUUJUVAUUOXLWHWEAUUPUVBUUSXPW
        HWEUVCAWIWJXLXPUNWKWLVOVPWMWNWOAXKXTVAXGAXIXOXJXSAYEWPLWQZCWPVBZXIXOUEZ
        AUUJUNXLUQUPWPXIWQZXLXIUICUEZLUUEUEZUVDUVEUVFWRZUUOAXLVQVBZUVGAYLXLXLUE
        UVKUVGUFUUCAXLWSBDFIJKXINXLPRSWTVNVGAYLYRYOUVHUUCUUMUUNBCDEFIJKXINOPQRS
        TUAXAWLUVIAUBWJUUJUVJUVGUVHUVIWRWPCLXIXNXLXNVIXBWCXCXDAYFWPMWQZUVEXJXSU
        EZAUUPUNXPUQUPWPXJWQZXPXJUICUEZMUUFUEZUVLUVEUVMWRZUUSAXPVQVBZUVNAYMXPXP
        UEUVRUVNUFUUDAXPWSBDFIJKXJNXPPRSWTVNVGAYMYTYOUVOUUDUURUUNBCDEFIJKXJNOPQ
        RSTUAXAWLUVPAUCWJUUPUVQUVNUVOUVPWRWPCMXJXRXPXRVIXBWCXCXDXEVOXF $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F f j k n s $.  $d F f z $.  $d G f j k n s $.  $d G f z $.  $d N n $.
      $d Q z $.  $d R f z $.  $d Z p $.
      arith1lemR5.3 $e |- S = { p e. Prime | p || Z } $.
      arith1lemR5.4 $e |- Q = sup ( S , RR , < ) $.
      arith1lemR5.5 $e |- N = ( ( # ` F ) - 1 ) $.
      arith1lemR5.6 $e |- G = ( F |` ( 1 ... N ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemR5 $p |- ( ( F e. R /\ Z = ( M ` F ) /\ Z e. ( ZZ>= ` 2 ) ) ->
                        ( G e. R /\ ( Z / Q ) = ( M ` G ) ) ) $=
        ( cK wcel cfv wceq c2 cuz w3a cdiv co chash c1 caddc cn0 wa simp1 cmin
        cc cn simp2 simp3 eleq1 biimpa syl11anc arith1lemH3 biimpar nncn syl
        ax1cn a1i npcan opreq1i syl5eq nnm1nn0 syl5eqel arith1lemE syl111anc
        simpld cmul arith1lemL2 fveq2d arith1lemP2 eqtrd opreq2d wb eqeq1
        mpbird cprime cdivides wbr cle wi arith1lemQ1 simp1d prmnn 3syl
        arith1lemK2 jca ) ICUCZMIKUDZUEZMUFUGUDZUCZUHZJCUCZMBUIUJJKUDZUEZXDXELJ
        UKUDUEZXDWSLULUMUJZIUKUDZUEZLUNUCZXEXHUOWSXAXCUPZXDXJULUQUJZULUMUJZXJXI
        XDXJURUCZULURUCZXOXJUEXDXJUSUCZXPXDWSWTXBUCZXRXMXDXAXCXSWSXAXCUTZWSXAXC
        VAZXAXCXSMWTXBVBVCVDWSXRXSACEFGHIKNPQVEVFVDZXJVGVHXQXDVIVJXJULVKVDLXNUL
        UMTVLVMZXDXNUNLXDXRXNUNUCYBXJVNVHTVOZACEFGHIJKLNPQUAVPVQVRZXDXGMXFBVSUJ
        ZUEZXDYGWTYFUEZXDWTXFXIIUDZVSUJZYFXDWSXKXLWTYJUEXMYCYDACEFGHIJKLNPQUAVT
        VQXDYIBXFVSXDYIXJIUDBXDXIXJIYCWAABCDEFGHIKMNOPQRSWBWCWDWCXDXAYGYHWEXTMW
        TYFWFVHWGXDXEXCBUSUCZXGYGWEYEYAXDXCBWHUCZYKYAXCYLBMWIWJUBDUCUBBWKWJWLBD
        UBMORSWMWNBWOWPABCEFGHJKMNPQWQVQWGWR $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d G f j k n s $.  $d G u v $.  $d G f z $.  $d H f j k n s $.  $d H v $.
      $d H f z $.  $d M g h u v y $.  $d Q g h u v y $.  $d Q g h z $.
      $d R f g h z $.  $d R g h u v y $.  $d Z g h u v y $.  $d Z p $.
      $d g h j k n s $.
      arith1lemR6.3 $e |- S = { p e. Prime | p || Z } $.
      arith1lemR6.4 $e |- Q = sup ( S , RR , < ) $.
      arith1lemR6.5 $e |- G = ( g |` ( 1 ... ( ( # ` g ) - 1 ) ) ) $.
      arith1lemR6.6 $e |- H = ( h |` ( 1 ... ( ( # ` h ) - 1 ) ) ) $.
      $( Lemma for ~ arith1 . $)
      arith1lemR6 $p |- ( Z e. ( ZZ>= ` 2 ) ->
      ( A. y e. NN ( y < Z -> A. g e. R A. h e. R
                     ( ( y = ( M ` g ) /\ y = ( M ` h ) ) -> g = h ) ) ->
      A. g e. R A. h e. R ( ( Z = ( M ` g ) /\ Z = ( M ` h ) ) -> g = h ) ) )
          $=
        ( vu vv c2 cuz cfv wcel cv clt wbr wceq wa wi wral cn cdiv co
        arith1lemQ2 simprd simpld breq1 eqeq1 anbi12d imbi1d 2ralbidv imbi12d
        rcla4v syl mpid fveq2 eqeq2d anbi1d anbi2d eqeq2 cbvral2v syl6ib
        rcla42v com12 syl6 impexp syl6ibr w3a simp2l simp3l simp1 chash c1
        cmin eqid arith1lemR5 syl111anc simp2r simp3r jca an4 sylib 3expib
        biid arith1lemR4 ex imim21d syld imbi2i ralbii syl5ibr r19.21advv
        syl5ib ) OUFUGUHUIZAUJZOUKULZXKUDUJZNUHZUMZXKUEUJZNUHZUMZUNZXMXPUMZUOZU
        EDUPUDDUPZUOZAUQUPZOGUJZNUHZUMZOHUJZNUHZUMZUNZYEYHUMZUOZHDUPGDUPXLXKYFU
        MZXKYIUMZUNZYLUOZHDUPGDUPZUOZAUQUPZXJYDYMGHDDXJYTYEDUIZYHDUIZUNZYMUOZYD
        XJYTUUCYKUNZYLUOZUUDXJYTLDUIZMDUIZUNZOCURUSZLNUHZUMZUUJMNUHZUMZUNZUNZLM
        UMZUOZUUFXJYTUUIUUOUUQUOZUOZUURXJYTUUJXNUMZUUJXQUMZUNZXTUOZUEDUPUDDUPZU
        UTXJYTUUJYFUMZUUJYIUMZUNZYLUOZHDUPGDUPZUVEXJYTUUJOUKULZUVJXJUUJUQUIZUVK
        CEOQTUAUTZVAXJUVLYTUVKUVJUOZUOXJUVLUVKUVMVBYSUVNAUUJUQXKUUJUMZXLUVKYRUV
        JXKUUJOUKVCUVOYQUVIGHDDUVOYPUVHYLUVOYNUVFYOUVGXKUUJYFVDXKUUJYIVDVEVFVGV
        HVIVJVKUVIUVDUVAUVGUNZXMYHUMZUOGHUDUEDDYEXMUMZUVHUVPYLUVQUVRUVFUVAUVGUV
        RYFXNUUJYEXMNVLZVMVNYEXMYHVDZVHYHXPUMZUVPUVCUVQXTUWAUVGUVBUVAUWAYIXQUUJ
        YHXPNVLZVMVOYHXPXMVPZVHVQVRUUIUVEUUSUVDUUSUULUVBUNZLXPUMZUOUDUELMDDXMLU
        MZUVCUWDXTUWEUWFUVAUULUVBUWFXNUUKUUJXMLNVLVMVNXMLXPVDVHXPMUMZUWDUUOUWEU
        UQUWGUVBUUNUULUWGXQUUMUUJXPMNVLVMVOXPMLVPVHVSVTWAUUIUUOUUQWBWCXJUUEUUPU
        UQYLXJUUCYKUUPXJUUCYKWDZUUGUULUNZUUHUUNUNZUNUUPUWHUWIUWJUWHUUAYGXJUWIXJ
        UUAUUBYKWEXJUUCYGYJWFXJUUCYKWGZBCDEFIJKYELNYEWHUHWIWJUSZOPQRSTUAUWLWKUB
        WLWMUWHUUBYJXJUWJXJUUAUUBYKWNXJUUCYGYJWOUWKBCDEFIJKYHMNYHWHUHWIWJUSZOPQ
        RSTUAUWMWKUCWLWMWPUUGUULUUHUUNWQWRWSXJUUCYKUUQYLUOUWHUUQYLUWHBCDEFGHIJK
        LMNOPQRSTUAUBUCUWHWTXAXBWSXCXDUUCYKYLWBVRYSYCAUQYRYBXLYQYAXOYOUNZUVQUOG
        HUDUEDDUVRYPUWNYLUVQUVRYNXOYOUVRYFXNXKUVSVMVNUVTVHUWAUWNXSUVQXTUWAYOXRX
        OUWAYIXQXKUWBVMVOUWCVHVQXEXFZXGXHUWOXI $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d F g h x $.  $d G h x $.  $d M g h x y $.  $d R f g h z $.
      $d R g h x y $.  $d f g h j k n s $.  $d g h p x y $.  $d p x z $.
      $( Lemma for ~ arith1 . $)
      arith1lemR8 $p |- ( ( F e. R /\ G e. R ) ->
                        ( ( M ` F ) = ( M ` G ) -> F = G ) ) $=
        ( vx vg vh vy vp wcel wa cfv cn wceq wi wf arith1lemB ffvelrn mpan
        adantr cv wrex wral fveq2 eqeq2d anbi1d eqeq1 imbi12d anbi2d eqeq2
        rcla42v c1 anbi12d imbi1d 2ralbidv c0 chash cc0 arith1lemA9 bi2anan9
        cfn wb arith1lemA4 hash0 syl bitr3d eqcom anbi12i syl5bbr eqtr3 syl6bi
        rgen2 cdivides wbr cprime crab cr clt csup cmin co cfz cres eqid
        arith1lemR6 indstr2 syl5 simpl biimpar jca imim1i syl6 exp4a r19.23adv
        risset syl5ib mpd ) GBRZHBRZSZGITZUARZXIHITZUBZGHUBZUCZXFXJXGBUAIUDXFXJ
        ABCDEFIJKLUEBUAGIUFUGUHXHMUIZXIUBZMUAUJXNXJXHXPXNMUAXHXOUARZXPXLXMXHXQX
        PXOXKUBZSZXMUCZXPXLSZXMUCXHXONUIZITZUBZXOOUIZITZUBZSZYBYEUBZUCZOBUKNBUK
        ZXTXQYJXTXPYGSZGYEUBZUCNOGHBBYBGUBZYHYLYIYMYNYDXPYGYNYCXIXOYBGIULUMUNYB
        GYEUOUPYEHUBZYLXSYMXMYOYGXRXPYOYFXKXOYEHIULUMUQYEHGURUPUSYKPUIZYCUBZYPY
        FUBZSZYIUCZOBUKNBUKUTYCUBZUTYFUBZSZYIUCZOBUKNBUKMPXOUTUBZYJUUDNOBBUUEYH
        UUCYIUUEYDUUAYGUUBXOUTYCUOXOUTYFUOVAVBVCXOYPUBZYJYTNOBBUUFYHYSYIUUFYDYQ
        YGYRXOYPYCUOXOYPYFUOVAVBVCUUDNOBBYBBRZYEBRZSZUUCYBVDUBZYEVDUBZSZYIUUIYC
        UTUBZYFUTUBZSZUULUUCUUIYBVETZVFUBZYEVETZVFUBZSUUOUULUUGUUQUUMUUHUUSUUNA
        BCDEFYBIJKLVGABCDEFYEIJKLVGVHUUGUUQUUJUUHUUSUUKUUGYBVIRUUQUUJVJABCDEFYB
        IJKLVKYBVLVMUUHYEVIRUUSUUKVJABCDEFYEIJKLVKYEVLVMVHVNUUMUUAUUNUUBYCUTVOY
        FUTVOVPVQYBYEVDVRVSVTPAQUIXOWAWBQWCWDZWEWFWGZBUUTCNODEFYBUTUUPUTWHWIWJW
        IWKZYEUTUURUTWHWIWJWIWKZIXOJQKLUUTWLUVAWLUVBWLUVCWLWMWNWOYAXSXMYAXPXRXP
        XLWPXPXRXLXOXIXKUOWQWRWSWTXAXBMXIUAXCXDXE $.
        $( [21-Nov-2012] $) $( [17-Nov-2012] $)
    $}

    ${
      $d M g h $.  $d M g z $.  $d R f z $.  $d R g h $.  $d f j k n s $.
      $( The Fundamental Theorem of Arithmetic, where a prime factorization is
         represented as a monotonic 1-based sequence of primes.  The function
         ` M ` maps the set of prime factorizations ` R ` one-to-one onto the
         positive integers.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
      arith1 $p |- M : R -1-1-onto-> NN $=
        ( vg vh cn wf1o wf1 wfo df-f1o wf cv cfv wceq wi wral dff13 arith1lemB
        arith1lemR8 rgen2 mpbir2an wrex dffo3 arith1lemQ9 rgen ) BMGNBMGOZBMGPZ
        BMGQUMBMGRZKSZGTZLSZGTUAUPURUAUBZLBUCKBUCKLBMGUDABCDEFGHIJUEZUSKLBBABCD
        EFUPURGHIJUFUGUHUNUOASZUQUAKBUIZAMUCKABMGUJUTVBAMABCKDEFGVAHIJUKULUHUH
        $.
        $( [17-Nov-2012] $)
    $}

    ${
      $d M g z $.  $d R f z $.  $d R g z $.  $d f j k n s $.
      $( The Fundamental Theorem of Arithmetic, where a prime factorization is
         represented as a monotonic 1-based sequence of primes.  Every positive
         integer has a unique prime factorization.  (Contributed by Paul
         Chapman, 17-Nov-2012.) $)
      arith12 $p |- A. z e. NN E! g e. R ( M ` g ) = z $=
        ( cv cfv wceq wreu cn wf1o wcel arith1 f1ofveu mpan rgen ) DLHMALZNDBOZ
        APBPHQUCPRUDABCEFGHIJKSDBPUCHTUAUB $.
        $( [17-Nov-2012] $)
    $}
  $}

$( (End of Paul Chapman's mathbox.) $)


