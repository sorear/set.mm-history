$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Group homomorphism and isomorphism
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B y $.  $d F w x y z $.  $d G x y $.  $d H w x y z $.
    $d M y z $.  $d S x y $.  $d T x y $.  $d X w x y z $.  $d Z w x y z $.
    ghomgrpilem1.1 $e |- G e. Grp $.
    ghomgrpilem1.2 $e |- H e. Grp $.
    ghomgrpilem1.3 $e |- F e. ( G GrpHom H ) $.
    ghomgrpilem1.4 $e |- X = ran G $.
    ghomgrpilem1.5 $e |- U = ( Id ` G ) $.
    ghomgrpilem1.6 $e |- N = ( inv ` G ) $.
    ghomgrpilem1.7 $e |- W = ran H $.
    ghomgrpilem1.8 $e |- T = ( Id ` H ) $.
    ghomgrpilem1.9 $e |- M = ( inv ` H ) $.
    ghomgrpilem1.10 $e |- Z = ran F $.
    ghomgrpilem1.11 $e |- S = ( H |` ( Z X. Z ) ) $.
    $( Lemma for ~ ghomgrpi . $)
    ghomgrpilem1 $p |- ( ( A e. X /\ B e. X ) ->
                         ( ( F ` A ) H ( F ` B ) ) = ( F ` ( A G B ) ) ) $=
      ( vy vx wcel cfv cv co wceq wral fveq2 opreq1d opreq1 fveq2d eqeq12d
      ralbidv wf cghom wa cgr wb elghom mp2an mpbi pm3.27i vtoclri opreq2d
      opreq2 rcla4v mpan9 ) ALUGAFUHZUEUIZFUHZHUJZAVNGUJZFUHZUKZUELULZBLUGVMBFU
      HZHUJZABGUJZFUHZUKZUFUIZFUHZVOHUJZWFVNGUJZFUHZUKZUELULZVTUFALWFAUKZWKVSUE
      LWMWHVPWJVRWMWGVMVOHWFAFUMUNWMWIVQFWFAVNGUOUPUQURLKFUSZWLUFLULZFGHUTUJUGZ
      WNWOVAZPGVBUGHVBUGWPWQVCNOUFUEFGHKLQTVDVEVFVGVHVSWEUEBLVNBUKZVPWBVRWDWRVO
      WAVMHVNBFUMVIWRVQWCFVNBAGVJUPUQVKVL $.
      $( [3-Mar-2008] $) $( [25-Feb-2008] $)

    $( Lemma for ~ ghomgrpi . $)
    ghomgrpilem2 $p |- S e. ( SubGrp ` H ) $=
      ( vx vy vz vw crn wf wss cv cfv co wceq wral cghom wcel wa cgr wb elghom
      mp2an mpbi pm3.26i frn ax-mp eqsstri wrex eleq2i wfn ffn fvelrnb bitri
      biimpi anim12i reeanv sylibr opreq12 eleq1d ghomgrpilem1 grpcl mp3an1
      dffn3 feq3 mpbir ffvelrni syl eqeltrd syl5cbi r19.23aivv cgi grpidcl
      grplid fveq2i eqtri ffvelrn eqid grpid eqtr4i eqeltri fveq2 grpinvcl
      mpan mpdan grprinv fveq2d eqtrd syl6eqr grpinvid1 sylanc mpbird
      r19.23aiv sylbi issubgi ) UCUDBFAGIKMRSTKDUGZIUAJIDUHZXNIUIXOUCUJZDUKUDUJ
      ZDUKFULXPXQEULDUKUMUDJUNUCJUNZDEFUOULUPZXOXRUQZNEURUPZFURUPZXSXTUSLMUCUDD
      EFIJORUTVAVBVCZJIDVDVEVFUBXPKUPZXQKUPZUQZUEUJZDUKZXPUMZUFUJZDUKZXQUMZUQZU
      FJVGUEJVGZXPXQFULZKUPZYFYIUEJVGZYLUFJVGZUQYNYDYQYEYRYDYQYDXPXNUPZYQKXNXPU
      AVHDJVIZYSYQUSXOYTYCJIDVJVEZUEJXPDVKVEVLZVMYEYRYEXQXNUPZYRKXNXQUAVHYTUUCY
      RUSUUAUFJXQDVKVEVLVMVNYIYLUEUFJJVOVPYMYPUEUFJJYMYHYKFULZKUPYPYGJUPZYJJUPZ
      UQZYMUUDYOKYHXPYKXQFVQVRUUGUUDYGYJEULZDUKZKYGYJABCDEFGHIJKLMNOPQRSTUAUBVS
      UUGUUHJUPZUUIKUPYAUUEUUFUUJLYGYJEJOVTWAJKUUHDJKDUHZJXNDUHZYTUULUUAJDWBVBK
      XNUMUUKUULUSUAKXNJDWCVEWDZWEWFWGWHWIWFBCDUKZKBFWJUKZUUNSUUNUUOUMZUUNUUNFU
      LZUUNUMZUUQCCEULZDUKZUUNCJUPZUVAUUQUUTUMYAUVALCEJOPWKVEZUVBCCABCDEFGHIJKL
      MNOPQRSTUAUBVSVAUUSCDYAUVAUUSCUMLUVBCCEJOPWLVAWMWNYBUUNIUPZUUPUURUSMXOUVA
      UVCYCUVBJICDWOVAUUNUUOFIRUUOWPWQVAWDWRZUUKUVAUUNKUPUUMUVBJKCDWOVAWSYDYQXP
      GUKZKUPZUUBYIUVFUEJYIYHGUKZKUPUVFUUEYIUVGUVEKYHXPGWTVRUUEUVGYGHUKZDUKZKUU
      EUVGUVIUMZYHUVIFULZBUMZUUEUVKUUNBUUEUVKYGUVHEULZDUKZUUNUUEUVHJUPZUVKUVNUM
      YAUUEUVOLYGEHJOQXAXBZYGUVHABCDEFGHIJKLMNOPQRSTUAUBVSXCUUEUVMCDYAUUEUVMCUM
      LYGCEHJOPQXDXBXEXFUVDXGYHIUPZUVIIUPZUVJUVLUSZUUEYBUVQUVRUVSMYHUVIBFGIRSTX
      HWAJIYGDYCWEUUEUVOUVRUVPJIUVHDYCWEWFXIXJUUEUVOUVIKUPUVPJKUVHDUUMWEWFWGWHX
      KXLXM $.
      $( [3-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    ghomgrpi.1 $e |- G e. Grp $.
    ghomgrpi.2 $e |- H e. Grp $.
    ghomgrpi.3 $e |- F e. ( G GrpHom H ) $.
    ghomgrpi.4 $e |- Y = ran F $.
    ghomgrpi.5 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( The image of a group homomorphism from ` G ` to ` H ` is a subgroup of
       ` H ` (inference version).  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    ghomgrpi $p |- S e. ( SubGrp ` H ) $=
      ( cgi cfv cgn crn eqid ghomgrpilem2 ) ADKLZCKLZBCDDMLZCMLZDNZCNZEFGHUBORO
      TOUAOQOSOIJP $.
      $( [3-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    $d A x y $.  $d G x y $.
    ghomsn.1 $e |- A e. _V $.
    ghomsn.2 $e |- G = { <. <. A , A >. , A >. } $.
    $( The endomorphism of the trivial group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    ghomsn $p |- ( _I |` { A } ) e. ( G GrpHom G ) $=
      ( vx vy cid csn cres cghom co wcel wf cv cfv wceq wral cgr wa wb cop
      grpsn eqeltri crn rneqi opex rnsnop eqtr2i elghom mp2an wf1o f1oi f1of
      ax-mp fveq2 snid fvresi syl6eq opreqan12d opreq12 eqtr4d elsn syl2anb
      grpcl mp3an1 syl rgen2a mpbir2an ) GAHZIZBBJKLZVIVIVJMZENZVJOZFNZVJOZBKZV
      MVOBKZVJOZPZFVIQEVIQZBRLZWBVKVLWASTBAAUAZAUAHZRDACUBUCZWEEFVJBBVIVIBUDWDU
      DVIBWDDUEWCAAAUFCUGUHZWFUIUJVIVIVJUKVLVIULVIVIVJUMUNVTEFVIVMVILZVOVILZSZV
      QVRVSVMAPZVOAPZVQVRPWGWHWJWKSVQAABKVRWJWKVNAVPABWJVNAVJOZAVMAVJUOAVILWLAP
      ACUPVIAUQUNZURWKVPWLAVOAVJUOWMURUSVMAVOABUTVAEAVBFAVBVCWIVRVILZVSVRPWBWGW
      HWNWEVMVOBVIWFVDVEVIVRUQVFVAVGVH $.
      $( [3-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    ghomgrplem.1 $e |- ( ph ->
                          ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ) $.
    ghomgrplem.2 $e |- S = { <. <. z , z >. , z >. } $.
    ghomgrplem.3 $e |- J = ( _I |` { z } ) $.
    $( Lemma for ~ ghomgrp . $)
    ghomgrplem $p |- ( ph -> ( H |` ( ran F X. ran F ) ) e. ( SubGrp ` H ) ) $=
      ( crn cxp cres csubg cfv wcel cif wceq reseq1 fveq2 eleq12d rneq xpeq1
      xpeq2 eqtrd reseq2 syl eleq1d cgr cghom co 3simp1d eleq1 cv cop csn
      visset grpsn eqeltri elimdhyp 3simp2d 3simp3d cid ghomsn elimdeloprv
      eqid ghomgrpi dedth2v ) AFDKZVILZMZFNOZPAFCQZVJMZVMNOZPVMADGQZKZVQLZMZVOP
      FDCGFVMRVKVNVLVOFVMVJSFVMNTUADVPRZVNVSVOVTVIVQRZVNVSRZDVPUBWAVJVRRWBWAVJV
      QVILVRVIVQVIUCVIVQVQUDUEVJVRVMUFUGUGUHVSVPAECQZVMVQAEUIPZWCUIPCUIPZECAWDF
      UIPZDEFUJUKPZHULEWCUIUMCWCUIUMCBUNZWHUOWHUOUPUIIWHBUQZURUSZUTAWFVMUIPWEFC
      AWDWFWGHVAFVMUIUMCVMUIUMWJUTAEFDUJCCGAWDWFWGHVBGVCWHUPMCCUJUKJWHCWIIVDUSV
      EVQVFVSVFVGVH $.
      $( [26-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    ghomgrp.1 $e |- Y = ran F $.
    ghomgrp.2 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( The image of a group homomorphism from ` G ` to ` H ` is a subgroup of
       ` H ` .  (Contributed by Paul Chapman, 25-Feb-2008.) $)
    ghomgrp $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                     S e. ( SubGrp ` H ) ) $=
      ( vx cgr wcel cghom co w3a crn cxp cres csubg cfv cv cop csn cid id eqid
      ghomgrplem wceq xpid11 reseq2 sylbir ax-mp eqtri syl5eqel ) CIJDIJBCDKLJM
      ZDBNZUNOZPZDQRAUMHHSZUQTUQTUAZBCDUBUQUAPZUMUCURUDUSUDUEADEEOZPZUPGEUNUFZV
      AUPUFZFVBUTUOUFVCEUNUGUTUODUHUIUJUKUL $.
      $( [26-Mar-2008] $) $( [25-Feb-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomfo.1 $e |- X = ran G $.
    ghomfo.2 $e |- Y = ran F $.
    ghomfo.3 $e |- S = ( H |` ( Y X. Y ) ) $.
    ghomfo.4 $e |- Z = ran S $.
    $( A group homomorphism maps onto its image.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomfo $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                   F : X -onto-> Z ) $=
      ( vx vy wfn crn wceq wfo cgr wcel cghom co w3a wa df-fo biimpri wf cv
      cfv wral eqid elghom biimp3a pm3.26d ffn syl cxp cres cdm wss csubg
      ghomgrp issubg sylib 3simp2d grpfo fof fdm 3syl dmeqi syl5reqr xpss12
      frn syl5ss sylanc wb sseq2d ssdmres syl5rbbr 3ad2ant2 mpbid eqtrd xpid11
      syl6req ) BENZBOZGPZEGBQZCRSZDRSZBCDTUASZUBZWGWDWFUCEGBUDUEWKEDOZBUFZWDWK
      WMLUGZBUHMUGZBUHDUAWNWOCUABUHPMEUILEUIZWHWIWJWMWPUCLMBCDWLEHWLUJZUKULUMZE
      WLBUNUOWKGFWEWKGGUPZFFUPZPGFPWKWSDWTUQZURZWTWKAURZWSXBWKARSZXCWSPZWKWIXDA
      DUSZWKADUTUHSWIXDXFUBABCDFIJVADAVBVCVDXDWSGAQWSGAUFXEAGKVEWSGAVFWSGAVGVHU
      OAXAJVIVJWKWTWLWLUPZUSZXBWTPZFWLUSZXJXHWKFWLFWLVKWKWEWLFWKWMWEWLUSWREWLBV
      LUOIVMZXKVNWIWHXHXIVOWJWIWTDURZUSXHXIWIXLXGWTWIXGWLDQXGWLDUFXLXGPDWLWQVEX
      GWLDVFXGWLDVGVHVPWTDVQVRVSVTWAGFWBVCIWCVN $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)

    $( Closure of a group homomorphism.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomcl $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                   ( A e. X -> ( F ` A ) e. Z ) ) $=
      ( cgr wcel cghom co w3a wfo wf cfv wi ghomfo fof ffvelrn ex 3syl ) DMNEMN
      CDEOPNQFHCRFHCSZAFNZACTHNZUABCDEFGHIJKLUBFHCUCUGUHUIFHACUDUEUF $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d A x y $.  $d B y $.  $d F x y $.  $d G x y $.  $d H x y $.  $d X x y $.
    ghomlin.1 $e |- X = ran G $.
    $( Linearity of a group homomorphism.  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomlin $p |- ( ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) /\
                      ( A e. X /\ B e. X ) ) ->
                    ( ( F ` A ) H ( F ` B ) ) = ( F ` ( A G B ) ) ) $=
      ( vx vy cgr wcel cghom co w3a cv cfv wceq wral wa crn wf eqid elghom
      biimp3a pm3.27d fveq2 opreq1d opreq1 fveq2d eqeq12d opreq2d opreq2
      rcla42v mpan9 ) DJKZEJKZCDELMKZNZHOZCPZIOZCPZEMZUSVADMZCPZQZIFRHFRZAFKBFK
      SACPZBCPZEMZABDMZCPZQZURFETZCUAZVGUOUPUQVOVGSHICDEVNFGVNUBUCUDUEVFVMVHVBE
      MZAVADMZCPZQHIABFFUSAQZVCVPVEVRVSUTVHVBEUSACUFUGVSVDVQCUSAVADUHUIUJVABQZV
      PVJVRVLVTVBVIVHEVABCUFUKVTVQVKCVABADULUIUJUMUN $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.
    ghomid.1 $e |- U = ( Id ` G ) $.
    ghomid.2 $e |- T = ( Id ` H ) $.
    $( A group homomorphism maps identity element to identity element.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    ghomid $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                   ( F ` U ) = T ) $=
      ( vx vy cgr wcel cghom co w3a cfv wceq crn wa eqid grpidcl 3ad2ant1 jca
      ghomlin mpdan grplid fveq2d eqtrd wb wf ffvelrn cv wral elghom biimp3a
      pm3.26d sylanc wi grpid ex 3ad2ant2 mpd mpbird ) DJKZEJKZCDELMKZNZBCOZAPZ
      VGVGEMZVGPZVFVIBBDMZCOZVGVFBDQZKZVNRVIVLPVFVNVNVCVDVNVEBDVMVMSZFTZUAZVQUB
      BBCDEVMVOUCUDVCVDVLVGPVEVCVKBCVCVNVKBPVPBBDVMVOFUEUDUFUAUGVFVGEQZKZVHVJUH
      ZVMVRCUIZVNVSVFVMVRBCUJVFWAHUKZCOIUKZCOEMWBWCDMCOPIVMULHVMULZVCVDVEWAWDRH
      ICDEVRVMVOVRSZUMUNUOVQUPVDVCVSVTUQVEVDVSVTVGAEVRWEGURUSUTVAVB $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F x y $.  $d G x y $.  $d H x y $.  $d S x y $.
    ghomgsg.1 $e |- Y = ran F $.
    ghomgsg.2 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( A group homomorphism from ` G ` to ` H ` is also a group homomorphism
       from ` G ` to its image in ` H ` .  (Contributed by Paul Chapman,
       3-Mar-2008.) $)
    ghomgsg $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
                    F e. ( G GrpHom S ) ) $=
      ( vx vy cgr wcel cghom co w3a crn wf cv cfv wceq wral wfo eqid ghomfo
      fof syl wa elghom biimp3a pm3.27d csubg wi ghomgrp subgopr ghomcl
      syl2and imp eqeq1d 2ralbidva mpbird biimprd 3adant3 wss issubg sylib
      3simp2d syld3an2 mp2and ) CJKZDJKZBCDLMKZNZCOZAOZBPZHQZBRZIQZBRZAMZVOVQCM
      BRZSZIVLTHVLTZBCALMKZVKVLVMBUAVNABCDVLEVMVLUBZFGVMUBZUCVLVMBUDUEVKWBVPVRD
      MZVTSZIVLTHVLTZVKVLDOZBPZWHVHVIVJWJWHUFHIBCDWIVLWDWIUBUGUHUIVKWAWGHIVLVLV
      KVOVLKZVQVLKZUFZUFVSWFVTVKWMVSWFSZVKVPVMKZVRVMKZWNWKWLVKADUJRKZWOWPUFWNUK
      ABCDEFGULZVPVRDAVMWEUMUEVOABCDVLEVMWDFGWEUNVQABCDVLEVMWDFGWEUNUOUPUQURUSV
      HAJKZVJVNWBUFZWCUKZVIVHWSXAVJVHWSUFWCWTHIBCAVMVLWDWEUGUTVAVKVIWSADVBZVKWQ
      VIWSXBNWRDAVCVDVEVFVG $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F x $.  $d G x $.  $d H x $.  $d T x $.  $d U x $.  $d X x $.  $d Z x $.
    ghomf1olem.1 $e |- X = ran G $.
    ghomf1olem.2 $e |- Y = ran F $.
    ghomf1olem.3 $e |- S = ( H |` ( Y X. Y ) ) $.
    ghomf1olem.4 $e |- Z = ran S $.
    ghomf1olem.5 $e |- U = ( Id ` G ) $.
    ghomf1olem.6 $e |- T = ( Id ` H ) $.

    ${
      $d F x y z $.  $d G y z $.  $d H y z $.  $d N x $.  $d T y z $.
      $d U y z $.  $d X y z $.
      ghomf1olem.7 $e |- N = ( inv ` G ) $.
      $( Lemma for ~ ghomf1o . $)
      ghomf1olem $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
          ( F : X -1-1-onto-> Z <-> A. x e. X ( ( F ` x ) = T -> x = U ) ) ) $=
        ( vy vz cgr wcel cghom co w3a wf1o cv cfv wceq wi wral grpidcl fveq2
        eqeq1d equequ1 imbi12d eqeq2d eqeq2 rcla42v expcom syl com23 3ad2ant1
        wf wf1 wa f1of1 dff13 sylib pm3.27d syl5 ghomid imbi1d imbi2d sylibd
        r19.21adv wfo df-f1o biimpri ghomfo adantr fof grpcl grpinvcl 3adant2
        syld3an3 3expib eqeq1 rcla4v syl6 imp opreq1 3ad2ant3 simprl 3ad2antl1
        adantrl jca ghomlin syldan 3adant3 simprr grprinv fveq2d 3eqtrd
        3eqtr3d 3expia wb grp2inv grpinvid2 3com23 bitr3d equcom syl5bbr 3expb
        biimprd imim12d syld ex r19.21aivv sylanc impbid ) FUAUBZGUAUBZEFGUCUDU
        BZUEZIKEUFZAUGZEUHZCUIZYGDUIZUJZAIUKZYEYFYKAIYEYFYGIUBZYHDEUHZUIZYJUJZU
        JZYMYKUJYESUGZEUHZTUGZEUHZUIZYRYTUIZUJZTIUKSIUKZYQYFYBYCUUEYQUJYDYBYMUU
        EYPYBDIUBZYMUUEYPUJZUJDFILPULYMUUFUUGUUDYPYHUUAUIZYGYTUIZUJSTYGDIIYRYGU
        IZUUBUUHUUCUUIUUJYSYHUUAYRYGEUMUNSATUOUPYTDUIZUUHYOUUIYJUUKUUAYNYHYTDEU
        MUQYTDYGURUPUSUTVAVBVCYFIKEVDZUUEYFIKEVEZUULUUEVFZIKEVGSTIKEVHZVIVJVKYE
        YPYKYMYEYOYIYJYEYNCYHCDEFGPQVLZUQVMVNVOVPYEYLYFUUMIKEVQZYFYEYLVFZYFUUMU
        UQVFIKEVRVSUULUUEUUMUURUUMUUNUUOVSUURUUQUULYEUUQYLBEFGIJKLMNOVTWAZIKEWB
        VAUURUUDSTIIYEYLYRIUBZYTIUBZVFZUUDUJYEUVBYLUUDYEUVBYLUUDUJYEUVBVFZYLYRY
        THUHZFUDZEUHZCUIZUVEDUIZUJZUUDYEUVBYLUVIUJZYEUVBUVEIUBZUVJYBYCUVBUVKUJY
        DYBUUTUVAUVKYBUUTUVDIUBZUVKUVAYRUVDFILWCYBUVAUVLUUTYTFHILRWDZWEZWFWGVCY
        KUVIAUVEIYGUVEUIZYIUVGYJUVHUVOYHUVFCYGUVEEUMUNYGUVEDWHUPWIWJWKUVCUUBUVG
        UVHUUCYEUVBUUBUVGYEUVBUUBUEYSUVDEUHZGUDZUUAUVPGUDZUVFCUUBYEUVQUVRUIUVBY
        SUUAUVPGWLWMYEUVBUVQUVFUIZUUBYEUVBUUTUVLVFUVSUVCUUTUVLYEUUTUVAWNYEUVAUV
        LUUTYBYCUVAUVLYDUVMWOWPZWQYRUVDEFGILWRWSWTYEUVBUVRCUIUUBUVCUVRYTUVDFUDZ
        EUHZYNCYEUVBUVAUVLVFUVRUWBUIUVCUVAUVLYEUUTUVAXAUVTWQYTUVDEFGILWRWSUVCUW
        ADEYEUVAUWADUIZUUTYBYCUVAUWCYDYTDFHILPRXBWOWPXCYEYNCUIUVBUUPWAXDWTXEXFU
        VCUUCUVHYBYCUVBUUCUVHXGZYDYBUUTUVAUWDYBUUTUVAUEZYTYRUIZUVHUUCUWEUVDHUHZ
        YRUIZUWFUVHUWEUWGYTYRYBUVAUWGYTUIUUTYTFHILRXHWEUNYBUUTUVLUWHUVHXGZUVAYB
        UVLUUTUWIUVDYRDFHILPRXIXJUVNWFXKTSXLXMXNWOXOXPXQXRVBWKXSXTUUSXTXRYA $.
        $( [3-Mar-2008] $) $( [3-Mar-2008] $)
    $}

    $( Two ways of saying a group homomorphism is 1-1-onto its image.
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    ghomf1o $p |- ( ( G e. Grp /\ H e. Grp /\ F e. ( G GrpHom H ) ) ->
        ( F : X -1-1-onto-> Z <-> A. x e. X ( ( F ` x ) = T -> x = U ) ) ) $=
      ( cgn cfv eqid ghomf1olem ) ABCDEFGFQRZHIJKLMNOPUAST $.
      $( [3-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F f $.  $d G f g h s $.  $d H f g h s $.
    $( Membership in the set of group isomorphisms from ` G ` to ` H ` .
       (Contributed by Paul Chapman, 25-Feb-2008.) $)
    elgiso $p |- ( ( G e. Grp /\ H e. Grp ) -> ( F e. ( G GrpIso H ) <->
        ( F e. ( G GrpHom H ) /\ F : ran G -1-1-onto-> ran H ) ) ) $=
      ( vf vg vh vs cgr wcel wa cgiso co crn cv wf1o cghom crab oprex rabex
      wceq wb rneq f1oeq2 syl rabbisdv opreq1 rabeq eqtrd f1oeq3 opreq2
      df-giso oprabval2 eleq2d f1oeq1 elrab syl6bb ) BHICHIJZABCKLZIABMZCMZDNZO
      ZDBCPLZQZIAVCIUSUTAOZJUQURVDAEFGBCHHENZMZFNZMZVAOZDVFVHPLZQZVDKUSVIVAOZDB
      VHPLZQZVBDVCBCPRSVFBTZVLVMDVKQZVOVPVJVMDVKVPVGUSTVJVMUAVFBUBVGUSVIVAUCUDU
      EVPVKVNTVQVOTVFBVHPUFVMDVKVNUGUDUHVHCTZVOVBDVNQZVDVRVMVBDVNVRVIUTTVMVBUAV
      HCUBVIUTUSVAUIUDUEVRVNVCTVSVDTVHCBPUJVBDVNVCUGUDUHDEFGUKULUMVBVEDAVCUSUTV
      AAUNUOUP $.
      $( [25-Feb-2008] $) $( [25-Feb-2008] $)
  $}


