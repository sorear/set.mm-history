$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Elementary Number Theory - Greatest common divisor (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Lemma for ~ mulgcd . $)
  mulgcdlem1 $p |- ( A = B ->
                ( ( ( 1st ` ( R ` A ) ) = ( Z x. ( 1st ` ( Q ` A ) ) ) /\
                  ( 2nd ` ( R ` A ) ) = ( Z x. ( 2nd ` ( Q ` A ) ) ) ) <->
                ( ( 1st ` ( R ` B ) ) = ( Z x. ( 1st ` ( Q ` B ) ) ) /\
                  ( 2nd ` ( R ` B ) ) = ( Z x. ( 2nd ` ( Q ` B ) ) ) ) ) ) $=
    ( wceq cfv c1st cmul co c2nd fveq2 fveq2d opreq2d eqeq12d anbi12d ) ABFZADG
    ZHGZEACGZHGZIJZFBDGZHGZEBCGZHGZIJZFRKGZETKGZIJZFUCKGZEUEKGZIJZFQSUDUBUGQRUC
    HABDLZMQUAUFEIQTUEHABCLZMNOQUHUKUJUMQRUCKUNMQUIULEIQTUEKUOMNOP $.
    $( [22-Jun-2011] $)

  ${
    $d K m $.  $d Q k m $.  $d R k m $.  $d Z k m $.  $d x y $.
    mulgcdlem2.1 $e |- S = ( NN0 X. NN0 ) $.
    mulgcdlem2.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    mulgcdlem2.3 $e |-
      Q = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { <. M , N >. } ) ) $.
    mulgcdlem2.4 $e |- R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0
                             ( NN0 X. { <. ( Z x. M ) , ( Z x. N ) >. } ) ) $.
    mulgcdlem2.5 $e |- M e. NN0 $.
    mulgcdlem2.6 $e |- N e. NN0 $.
    mulgcdlem2.7 $e |- Z e. NN $.
    $( Lemma for ~ mulgcd . $)
    mulgcdlem2 $p |- ( K e. NN0 ->
                  ( ( 1st ` ( R ` K ) ) = ( Z x. ( 1st ` ( Q ` K ) ) ) /\
                    ( 2nd ` ( R ` K ) ) = ( Z x. ( 2nd ` ( Q ` K ) ) ) ) ) $=
      ( vm vk cv cfv c1st cmul co wceq c2nd wa cc0 c1 caddc mulgcdlem1 cop
      oprex op1st wcel cn0 cxp cn nnnn0 ax-mp nn0mulcl mp2an opelxpi eleqtrri
      cvv nn0ex xpex eqeltri wf eucalgf feq23i mpbir algr0 fveq2i elisseti
      eqtri opreq2i 3eqtr4i op2nd pm3.2i wi eqtr3 cmo cif algrf ffvelrni
      syl6eleq eucalgval syl iftrue sylan9eq fveq2d opreq2d sylan2 expcom eqtr
      sylan ex sylan9 anandis anim12d wn wb eqeq1 adantl wne cc xp2nd nn0cn
      3syl nncn mulne0b mpan nnne0 biantrur syl5bb nebi sylibr adantr bitr4d
      biimprd iman sylib pm2.21d adantrl com12 exp3a biimpd ancom notbii w3a
      iffalse fvex syl6eq eqeq1d eqeq2d adantrr bitr2d adantld 3impia elxp6
      simplbi df-opr syl6reqr opreq12 sylan9req 3adant3 cr crp xp1st nn0re clt
      wbr cle nn0ge0 0re leltne mp3an1 syl11anc biimpar elrp biimpri syldan
      df-ne sylan2br nnrp moddi eqtr3d 3adant2 eqtr4d eqeqan12rd mpbird 3com23
      jca 3expia 4cases algrp1 eqeq12d anbi12d sylibrd nn0ind ) RTZDUAZUBUAJUWB
      CUAZUBUAUCUDUEUWCUFUAJUWDUFUAUCUDUEUGUHDUAZUBUAZJUHCUAZUBUAZUCUDZUEZUWEUF
      UAZJUWGUFUAZUCUDZUEZUGSTZDUAZUBUAZJUWOCUAZUBUAZUCUDZUEZUWPUFUAZJUWRUFUAZU
      CUDZUEZUGZUWOUIUJUDZDUAZUBUAZJUXGCUAZUBUAZUCUDZUEZUXHUFUAZJUXJUFUAZUCUDZU
      EZUGZGDUAZUBUAJGCUAZUBUAUCUDUEUXSUFUAJUXTUFUAUCUDUEUGRSGUWBUHCDJUKUWBUWOC
      DJUKUWBUXGCDJUKUWBGCDJUKUWJUWNJHUCUDZJIUCUDZULZUBUAUYAUWFUWIUYAUYBJHUCUMZ
      UNUWEUYCUBUYCEUOZUWEUYCUEUYCUPUPUQZEUYAUPUOZUYBUPUOZUYCUYFUOJUPUOZHUPUOZU
      YGJURUOZUYIQJUSUTZOJHVAVBUYIIUPUOZUYHUYLPJIVAVBUYAUYBUPUPVCVBKVDZUYCDEFEU
      YFVEKUPUPVFVFVGVHZEEFVIUYFUYFFVIABFLVJEEUYFUYFFKKVKVLZNVMUTZVNUWHHJUCUWHH
      IULZUBUAHUWGUYRUBUYREUOZUWGUYRUEUYRUYFEUYJUYMUYRUYFUOOPHIUPUPVCVBKVDZUYRC
      EFUYOUYPMVMUTZVNHIHUPOVOZUNVPVQVRUYCUFUAUYBUWKUWMUYAUYBUYDJIUCUMVSUWEUYCU
      FUYQVNUWLIJUCUWLUYRUFUAIUWGUYRUFVUAVNHIVUBIUPPVOVSVPVQVRVTUWOUPUOZUXFUWPF
      UAZUBUAZJUWRFUAZUBUAZUCUDZUEZVUDUFUAZJVUFUFUAZUCUDZUEZUGZUXRUXCUHUEZUXBUH
      UEZVUCUXFVUNWAZWAVUCVUOVUPUGZVUQVUCVURUGUXAVUIUXEVUMVUCVUOVUPUXAVUIWAVUCV
      UOUGZUXAUWQVUHUEZVUCVUPUGZVUIUXAVUSVUTUXAVUHUWTUEVUTVUSUWQVUHUWTWBVUSVUGU
      WSJUCVUSVUFUWRUBVUCVUOVUFVUOUWRUXCUWRWCUAZULZWDZUWRVUCUWRUYFUOZVUFVVDUEVU
      CUWREUYFUPEUWOCUYSUPECVIUYTUYRCEFUYOUYPMWEUTWFKWGZABFUWRLWHWIZVUOUWRVVCWJ
      WKZWLWMWNWOVVAVUTVUIVUEUWQUEVUTVUIVVAVUEUWQVUHWPVVAVUDUWPUBVUCVUPVUDVUPUW
      PUXBUWPWCUAZULZWDZUWPVUCUWPUYFUOZVUDVVKUEVUCUWPEUYFUPEUWODUYEUPEDVIUYNUYC
      DEFUYOUYPNWEUTWFKWGZABFUWPLWHWIZVUPUWPVVJWJWKZWLWQWRWSWTVUCVUOVUPUXEVUMWA
      VUSUXEUXBVULUEZVVAVUMUXEVUSVVPUXEVULUXDUEVVPVUSUXBVULUXDWBVUSVUKUXCJUCVUS
      VUFUWRUFVVHWLWMWNWOVVAVVPVUMVUJUXBUEVVPVUMVVAVUJUXBVULWPVVAVUDUWPUFVVOWLW
      QWRWSWTXAWOVUOVUPXBZUGZVUCUXFVUNVUCUXFUGZVVRVUNVUCUXEVVRVUNWAUXAVUCUXEUGZ
      VVRVUNVVTVUOVUPWAVVRXBVVTVUPVUOVVTVUPUXDUHUEZVUOUXEVUPVWAXCVUCUXBUXDUHXDX
      EVUCVUOVWAXCZUXEVUCUXCUHXFZUXDUHXFZXCZVWBVUCUXCXGUOZVWEVUCVVEUXCUPUOZVWFV
      VFUWRUPUPXHZUXCXIXJVWFJUHXFZVWCUGZVWDVWCJXGUOZVWFVWJVWDXCUYKVWKQJXKUTJUXC
      XLXMVWIVWCUYKVWIQJXNUTXOXPWIUXCUHUXDUHXQXRXSXTZYAVUOVUPYBYCYDYEYFYGVUOXBZ
      VUPUGZVUCUXFVUNVVSVWNVUNVUCUXEVWNVUNWAUXAVVTVWNVUNVVTVUPVWMUGZXBZVWNXBVVT
      VUPVUOWAVWPVVTVUPVUOVWLYHVUPVUOYBYCVWOVWNVUPVWMYIYJYCYDYEYFYGVUCVWMVVQUGZ
      VUQVUCVWQUXFVUNVUCVWQUXFYKVUIVUMVUCVWQUXFVUIVUCVWQUGZUXEVUIUXAVWRUXEVUIVW
      RVUIUXBVUHUEZUXEVUCVVQVUIVWSXCVWMVUCVVQUGZVUEUXBVUHVWTVUEVVJUBUAUXBVWTVUD
      VVJUBVUCVVQVUDVVKVVJVVNVUPUWPVVJYLWKZWLUXBVVIUWPUFYMZUNYNYOYEVUCVWMVWSUXE
      XCVVQVUCVWMUGZVUHUXDUXBVXCVUGUXCJUCVXCVUGVVCUBUAUXCVXCVUFVVCUBVUCVWMVUFVV
      DVVCVVGVUOUWRVVCYLWKZWLUXCVVBUWRUFYMZUNYNWMYPYQYRYHYSYTVUCUXFVWQVUMVUCUXF
      VWQYKZVUMVVIJVVBUCUDZUEZVXFVVIUWTUXDWCUDZVXGVUCUXFVVIVXIUEVWQVUCUXFVVIUWQ
      UXBWCUDZVXIVUCVVIUWQUXBULZWCUAVXJVUCUWPVXKWCVUCVVLUWPVXKUEZVVMVVLVXLUWQUP
      UOUXBUPUOUGUWPUPUPUUAUUBWIWLUWQUXBWCUUCUUDUWQUWTUXBUXDWCUUEUUFUUGVUCVWQVX
      GVXIUEZUXFVUCVWMVXMVVQVXCJUWSUXCWCUDZUCUDZVXGVXIVUCVXOVXGUEVWMVUCVXNVVBJU
      CVUCVVBUWSUXCULZWCUAVXNVUCUWRVXPWCVUCVVEUWRVXPUEZVVFVVEVXQUWSUPUOZVWGUGUW
      RUPUPUUAUUBWIWLUWSUXCWCUUCUUDWMXSVXCUWSUUHUOZUXCUUIUOZVXOVXIUEZVUCVXSVWMV
      UCVVEVXRVXSVVFUWRUPUPUUJUWSUUKXJXSVWGVWMVXTVUCVWGVWCVXTVWMVWGVWCUHUXCUULU
      UMZVXTVWGVYBVWCVWGUXCUUHUOZUHUXCUUNUUMZVYBVWCXCZUXCUUKZUXCUUOUHUUHUOVYCVY
      DVYEUUPUHUXCUUQUURUUSUUTVYCVYBVXTVWGVXTVYCVYBUGUXCUVAUVBVYFWQUVCUXCUHUVDU
      VEVUCVVEVWGVVFVWHWIWQJUUIUOZVXSVXTVYAUYKVYGQJUVFUTJUWSUXCUVGUURUUSUVHYQUV
      IUVJVUCVWQVUMVXHXCZUXFVUCVWMVVQVYHVWTVXCVUJVVIVULVXGVWTVUJVVJUFUAVVIVWTVU
      DVVJUFVXAWLUXBVVIVXBUWPWCYMVSYNVXCVUKVVBJUCVXCVUKVVCUFUAVVBVXCVUFVVCUFVXD
      WLUXCVVBVXEUWRWCYMVSYNWMUVKWTUVIUVLUVMUVNUVOWOUVPVUCUXMVUIUXQVUMVUCUXIVUE
      UXLVUHVUCUXHVUDUBUYEVUCUXHVUDUEUYNUYCDEFUWOUYOUYPNUVQXMZWLVUCUXKVUGJUCVUC
      UXJVUFUBUYSVUCUXJVUFUEUYTUYRCEFUWOUYOUYPMUVQXMZWLWMUVRVUCUXNVUJUXPVULVUCU
      XHVUDUFVYIWLVUCUXOVUKJUCVUCUXJVUFUFVYJWLWMUVRUVSUVTUWA $.
      $( [22-Jun-2011] $)
  $}

  $( Lemma for ~ mulgcd . $)
  mulgcdlem3 $p |- ( ( M e. NN /\ N e. NN0 ) -> ( M x. N ) e. ( ZZ>= ` N ) ) $=
    ( cn wcel cn0 wa cmul co cuz cfv cle wbr c1 wceq cc nn0cn mulid2 syl
    adantl nnge1 adantr cr cc0 wi 1re w3a lemul1a ex mp3an1 nnre nn0re nn0ge0
    jca syl2an mpd eqbrtrrd cz wb eluz zmulcl sylan2 anabss7 nnz nn0z mpbird )
    ACDZBEDZFZABGHZBIJDZBVIKLZVHMBGHZBVIKVGVLBNZVFVGBODVMBPBQRSVHMAKLZVLVIKLZVF
    VNVGATUAAUBDZBUBDZUCBKLZFZVNVOUDZVFVGMUBDZVPVSVTUEWAVPVSUFVNVOMABUGUHUIAUJV
    GVQVRBUKBULUMUNUOUPAUQDZBUQDZVJVKURZVFVGWBWCWDWCVIUQDWDWBWCFBVIUSABUTVAVBAV
    CBVDUNVE $.
    $( [22-Jun-2011] $)

  ${
    $d x y $.
    mulgcdlem4.1 $e |- S = ( NN0 X. NN0 ) $.
    mulgcdlem4.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    mulgcdlem4.3 $e |-
      Q = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { <. M , N >. } ) ) $.
    mulgcdlem4.4 $e |- R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0
                             ( NN0 X. { <. ( Z x. M ) , ( Z x. N ) >. } ) ) $.
    mulgcdlem4.5 $e |- M e. NN0 $.
    mulgcdlem4.6 $e |- N e. NN0 $.
    mulgcdlem4.7 $e |- Z e. NN $.
    $( Lemma for ~ mulgcd . $)
    mulgcdlem4 $p |- ( K e. ( ZZ>= ` ( Z x. N ) ) ->
                  ( ( 2nd ` ( Q ` K ) ) = 0 /\ ( 2nd ` ( R ` K ) ) = 0 ) ) $=
      ( cmul co cuz cfv wcel c2nd cc0 wceq cn cn0 mulgcdlem3 mp2an uztrn mpan2
      cop elisseti op2nd fveq2i eleq2i wi cxp opelxpi eleqtrri eqid eucalgcvga
      ax-mp sylbir syl oprex nnnn0 nn0mulcl jca ) GJIRSZTUAZUBZGCUAUCUAUDUEZGDU
      AUCUAUDUEZVLGITUAZUBZVMVLVJVOUBZVPJUFUBZIUGUBZVQQPJIUHUIVJGIUJUKVPGHIULZU
      CUAZTUAZUBZVMWBVOGWAITHIHUGOUMIUGPUMUNUOUPVTEUBWCVMUQVTUGUGURZEHUGUBZVSVT
      WDUBOPHIUGUGUSUIKUTABVTCEFGWAKLMWAVAVBVCVDVEVLGJHRSZVJULZUCUAZTUAZUBZVNWI
      VKGWHVJTWFVJJHRVFJIRVFUNUOUPWGEUBWJVNUQWGWDEWFUGUBZVJUGUBZWGWDUBJUGUBZWEW
      KVRWMQJVGVCZOJHVHUIWMVSWLWNPJIVHUIWFVJUGUGUSUIKUTABWGDEFGWHKLNWHVAVBVCVDV
      I $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d E z $.  $d K z $.  $d N z $.  $d Q z $.  $d S z $.  $d Z z $.  $d x y $.
    mulgcdlem5.1 $e |- S = ( NN0 X. NN0 ) $.
    mulgcdlem5.2 $e |- E = { <. x , y >. | ( x e. ( NN0 X. NN0 ) /\
      y = if ( ( 2nd ` x ) = 0 , x , <. ( 2nd ` x ) , ( mod ` x ) >. ) ) } $.
    mulgcdlem5.3 $e |-
      Q = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { <. M , N >. } ) ) $.
    mulgcdlem5.4 $e |- R = ( ( E o. ( 1st |` ( S X. S ) ) ) seq0
                             ( NN0 X. { <. ( Z x. M ) , ( Z x. N ) >. } ) ) $.
    mulgcdlem5.5 $e |- M e. NN0 $.
    mulgcdlem5.6 $e |- N e. NN0 $.
    mulgcdlem5.7 $e |- Z e. NN $.
    $( Lemma for ~ mulgcd . $)
    mulgcdlem5 $p |- ( ( Z x. M ) gcd ( Z x. N ) ) = ( Z x. ( M gcd N ) ) $=
      ( vz cmul co cfv c1st cgcd cn0 wcel wceq cn nnnn0 ax-mp nn0mulcl mp2an
      c2nd mulgcdlem2 simplld cop eqid eucalg cxp cuz opelxpi mulgcdlem3 cres
      nn0ex xpex eucalgf ccom csn cseq0 xpeq12i reseq2i coeq2i opreq1i eqtri
      f2ndres cv cc0 wne clt wbr wi eucalglt ffvelrni fvres syl neeq1d breq12d
      imbi12d mpbird elisseti op2nd eqtr2i eqeq1d cmo cif eucalgval iftrue
      sylan9eq ex sylbid algfx mp2 fveq2i opreq2i 3eqtr3i ) IHRSZDTZUATZIXDCTZU
      ATZRSZIGRSZXDUBSZIGHUBSZRSXDUCUDZXFXIUEZIUCUDZHUCUDZXMIUFUDZXOPIUGUHZOIHU
      IUJZXMXNXEUKTIXGUKTRSUEABCDEFXDGHIJKLMNOPULUMUHXJUCUDZXMXFXKUEXOGUCUDZXTX
      RNIGUIUJXSABXJXDUNZDEFXJXDJKMYBUOUPUJXHXLIRXHHCTZUATZXLXGYCUAGHUNZUCUCUQZ
      UDZXDHURTUDZXGYCUEYAXPYGNOGHUCUCUSUJZXQXPYHPOIHUTUJQYEUKYFVAZCYFFXDHUCUCV
      BVBVCABFKVDZCFUAEEUQZVAZVEZUCYEVFUQZVGSFUAYFYFUQZVAZVEZYOVGSLYNYRYOVGYMYQ
      FYLYPUAEYFEYFJJVHVIVJVKVLUCUCVMQVNZYFUDZYSFTZYJTZVOVPZUUBYSYJTZVQVRZVSUUA
      UKTZVOVPZUUFYSUKTZVQVRZVSABFYSKVTYTUUCUUGUUEUUIYTUUBUUFVOYTUUAYFUDUUBUUFU
      EYFYFYSFYKWAUUAYFUKWBWCZWDYTUUBUUFUUDUUHVQUUJYSYFUKWBZWEWFWGYEYJTZYEUKTZH
      YGUULUUMUEYIYEYFUKWBUHGHGUCNWHHUCOWHWIWJYTUUDVOUEUUHVOUEZUUAYSUEZYTUUDUUH
      VOUUKWKYTUUNUUOYTUUNUUAUUNYSUUHYSWLTUNZWMYSABFYSKWNUUNYSUUPWOWPWQWRWSWTXA
      YAXPYDXLUENOABYECEFGHJKLYEUOUPUJVLXBXC $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d x y $.
    $( Lemma for ~ mulgcd . $)
    mulgcdlem6 $p |- ( ( K e. NN /\ M e. NN0 /\ N e. NN0 ) ->
                     ( ( K x. M ) gcd ( K x. N ) ) = ( K x. ( M gcd N ) ) ) $=
      ( vx vy cn wcel cn0 cmul co cgcd wceq c1 cif cc0 opreq1 opreq12d eqeq12d
      opreq2 opreq1d opreq2d cv cxp c2nd cfv cmo cop wa copab c1st cres ccom
      csn cseq0 eqid 0nn0 elimel 1nn mulgcdlem5 dedth3h ) AFGZBHGZCHGZABIJZACIJ
      ZKJZABCKJZIJZLVAAMNZBIJZVICIJZKJZVIVGIJZLVIVBBONZIJZVKKJZVIVNCKJZIJZLVOVI
      VCCONZIJZKJZVIVNVSKJZIJZLABCMOOAVILZVFVLVHVMWDVDVJVEVKKAVIBIPAVICIPQAVIVG
      IPRBVNLZVLVPVMVRWEVJVOVKKBVNVIISTWEVGVQVIIBVNCKPUARCVSLZVPWAVRWCWFVKVTVOK
      CVSVIISUAWFVQWBVIICVSVNKSUARDEDUBZHHUCZGEUBWGUDUEZOLWGWIWGUFUEUGNLUHDEUIZ
      UJWHWHUCUKULZHVNVSUGUMUCUNJZWKHVOVTUGUMUCUNJZWHWJVNVSVIWHUOWJUOWLUOWMUOBO
      HUPUQCOHUPUQAMFURUQUSUT $.
      $( [22-Jun-2011] $)
  $}

  $( Lemma for ~ mulgcd . $)
  mulgcdlem7 $p |- ( ( K e. NN0 /\ M e. NN0 /\ N e. NN0 ) ->
                     ( ( K x. M ) gcd ( K x. N ) ) = ( K x. ( M gcd N ) ) ) $=
    ( cn0 wcel cmul co cgcd wceq wa cc0 clt wbr wo cle nn0ge0 cr wb nn0re 0re
    leloe mpan syl mpbid adantr cn mulgcdlem6 3expb cz elnnz biimpri nn0z
    sylan ancoms anasss expcom wi cc nn0cn mul02 opreq1 sylan9req opreqan12d
    anandirs gcd0val syl5reqr gcdcl 3syl syl2an eqtrd ex adantl jaod mpd 3impb
    ) ADEZBDEZCDEZABFGZACFGZHGZABCHGZFGZIZVPVQVRJZJZKALMZKAIZNZWDVPWIWEVPKAOMZW
    IAPVPAQEZWJWIRZASKQEWKWLTKAUAUBUCUDUEWFWGWDWHWGWFWDWGVPWEWDAUFEZWEWDWGVPJWM
    VQVRWDABCUGUHVPWGWMAUIEZWGWMVPWMWNWGJAUJUKAULUMUNUMUOUPWEWHWDUQVPWEWHWDWEWH
    JZWAKWCWOKKHGZWAKVQVRWHWPWAIVQWHJVRWHJKVSKVTHVQWHKKBFGZVSVQBUREWQKIBUSBUTUC
    KABFVAVBVRWHKKCFGZVTVRCUREWRKICUSCUTUCKACFVAVBVCVDVEVFWEWHKKWBFGZWCBUIEZCUI
    EZWSKIZVQVRWTXAJWBDEWBUREXBBCVGWBUSWBUTVHBULCULVIKAWBFVAVBVJVKVLVMVNVO $.
    $( [22-Jun-2011] $)

  $( Distribute multiplication by a nonnegative integer over gcd.  The proof
     involves "running" two copies of Euclid's Algorithm side by side, one
     calculating ` ( M gcd N ) ` and the other
     ` ( ( K x. M ) gcd ( K x. N ) ) ` , and tracking the multiplicative
     relationship maintained at each step.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  mulgcd $p |- ( ( K e. NN0 /\ M e. ZZ /\ N e. ZZ ) ->
                 ( ( K x. M ) gcd ( K x. N ) ) = ( K x. ( M gcd N ) ) ) $=
    ( cn0 wcel cz w3a cmul co cgcd cabs cfv wceq wa cc absmul nn0cn zcn syl2an
    cr cc0 cle wbr nn0re nn0ge0 absid syl11anc opreq1d adantr eqtrd opreqan12d
    3impdi gcdabs zmulcl nn0z syl3an1 mulgcdlem7 id nn0abscl syl3an 3eqtr3d
    opreq2d 3adant1 ) ADEZBFEZCFEZGZABHIZACHIZJIZABKLZCKLZJIZHIZABCJIZHIZVGVHKL
    ZVIKLZJIZAVKHIZAVLHIZJIZVJVNVDVEVFVSWBMVDVENZVDVFNZVQVTVRWAJWCVQAKLZVKHIZVT
    AOEZBOEVQWFMVDVEABPAQZBRSVDWFVTMVEVDWEAVKHVDATEUAAUBUCWEAMAUDAUEAUFUGZUHUIU
    JWDVRWEVLHIZWAWGCOEVRWJMVDVFACPWHCRSVDWJWAMVFVDWEAVLHWIUHUIUJUKULAFEZVEVFVS
    VJMZVDWKVEVFWLVHFEVIFEWLWKVENWKVFNVHVIUMABUNACUNSULAUOUPVDVKDEVLDEWBVNMVDVE
    VFAVKVLUQVDURBUSCUSUTVAVEVFVNVPMVDVEVFNVMVOAHBCUMVBVCUJ $.
    $( [22-Jun-2011] $)

  $( Distribute absolute value of multiplication over gcd.  (Contributed by
     Paul Chapman, 22-Jun-2011.) $)
  absmulgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
      ( ( K x. M ) gcd ( K x. N ) ) = ( abs ` ( K x. ( M gcd N ) ) ) ) $=
    ( cz wcel w3a cabs cfv cgcd co cmul wceq wa cn0 gcdcl cr cc0 cle wbr nn0re
    nn0ge0 absid syl11anc syl opreq2d 3adant1 cc absmul zcn nn0cn syl2an 3impb
    opreqan12d 3impdi syl3an gcdabs zmulcl mulgcd nn0abscl nn0z 3eqtr3d eqtrd
    3eqtr4rd ) ADEZBDEZCDEZFZAGHZBCIJZGHZKJZVHVIKJZAVIKJGHZABKJZACKJZIJZVEVFVKV
    LLVDVEVFMZVJVIVHKVQVINEZVJVILZBCOZVRVIPEQVIRSVSVITVIUAVIUBUCUDUEUFVDVEVFVMV
    KLZAUGEZVIUGEZWAVDVQAVIUHAUIZVQVRWCVTVIUJUDUKULVGVPVHBGHZCGHZIJZKJZVLVGVNGH
    ZVOGHZIJZVHWEKJZVHWFKJZIJZVPWHWBBUGEZCUGEZWKWNLZVDVEVFWBWOWPWQWBWOMWBWPMWIW
    LWJWMIABUHACUHUMUNWDBUICUIUOVDVEVFWKVPLZVNDEVODEWRVDVEMVDVFMVNVOUPABUQACUQU
    KUNVHNEWEDEZWFDEZWNWHLVDVEVFVHWEWFURAUSVEWENEWSBUSWEUTUDVFWFNEWTCUSWFUTUDUO
    VAVGWGVIVHKVEVFWGVILVDBCUPUFUEVBVC $.
    $( [22-Jun-2011] $)

  ${
    $d K x y $.  $d M x y $.  $d N x y $.
    $( An integer which divides each of two others also divides their gcd.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    dvdsgcd $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
                     ( ( K || M /\ K || N ) -> K || ( M gcd N ) ) ) $=
      ( vx vy cz wcel w3a cdivides wbr wa cv cmul co wceq wrex cgcd wb divides
      bi2anan9 3impdi reeanv syl6bbr wi cabs cfv dvdsmul1 cn0 gcdcl nn0z syl
      sylan2 dvdsabsb zmulcl anabss5 mpbid 3impb adantr cc mulcom eqeq1d zcn
      syl3an biimpar opreq12 absmulgcd eqtr3d breq2d mpbird ex 3expib
      r19.23advv 3ad2ant1 sylbid ) AFGZBFGZCFGZHZABIJZACIJZKZDLZAMNZBOZELZAMNZC
      OZKZEFPDFPZABCQNZIJZVRWAWDDFPZWGEFPZKZWIVOVPVQWAWNRVOVPKVSWLVOVQKVTWMDABS
      EACSTUAWDWGDEFFUBUCVOVPWIWKUDVQVOWHWKDEFFVOWBFGZWEFGZWHWKUDVOWOWPHZWHWKWQ
      WHKZWKAAWBWEQNZMNZUEUFZIJZWQXBWHVOWOWPXBVOWOWPKZKZAWTIJZXBVOWSFGZXEXCAWSU
      GXCWSUHGXFWBWEUIWSUJUKZULVOXCXEXBRZVOWTFGZXHXDAWTUMVOXFXIXCAWSUNXGULULUOU
      PUQURWRWJXAAIWRAWBMNZAWEMNZQNZWJXAWRXJBOZXKCOZKZXLWJOWQXOWHAUSGZWBUSGZWEU
      SGZXOWHRZVOWOWPXPXQXRXSXPXQKZXMWDXPXRKZXNWGXTXJWCBAWBUTVAYAXKWFCAWEUTVATU
      AAVBWBVBWEVBVCVDXJBXKCQVEUKWQXLXAOWHAWBWEVFURVGVHVIVJVKVLVMVN $.
      $( [22-Jun-2011] $)
  $}

