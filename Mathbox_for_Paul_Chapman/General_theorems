$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      General theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Simplify an implication between two implications when the antecedent of
     the first is a consequence of the antecedent of the second.  The reverse
     form is useful in producing the successor step in induction proofs.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  imim21b $p |- ( ( ps -> ph ) -> ( ( ( ph -> ch ) -> ( ps -> th ) ) <->
                                    ( ps -> ( ch -> th ) ) ) ) $=
    ( wi wa wb pm5.5 imim2i pm5.32rd ancom syl6bb imbi1d impexp 3bitr3g ) BAEZA
    CEZBFZDEBCFZDEQBDEEBCDEEPRSDPRCBFSPBQCAQCGBACHIJCBKLMQBDNBCDNO $.
    $( [22-Jun-2011] $)

  $( A function is equinumerate to its domain.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  fndmeng $p |- ( ( F Fn A /\ A e. C ) -> A ~~ F ) $=
    ( wfn wcel wa cdm cen wbr cvv wfun fnex fnfun adantr wi c0 cif wceq funeq
    dmeq id breq12d imbi12d 0ex elimel fundmen dedth imp syl11anc wb fndm
    breq1d mpbid ) CADZABEZFZCGZCHIZACHIZUPCJEZCKZURABCLUNVAUOACMNUTVAURUTVAURO
    UTCPQZKZVBGZVBHIZOCPCVBRZVAVCURVECVBSVFUQVDCVBHCVBTVFUAUBUCVBCPJUDUEUFUGUHU
    IUNURUSUJUOUNUQACHACUKULNUM $.
    $( [22-Jun-2011] $)

  ${
    $d A x y $.  $d B y $.  $d C x y $.  $d D x y $.  $d ph x y $.
    $( Conditional operator existence.  (Contributed by Paul Chapman,
       21-Mar-2011.) $)
    ifexg $p |- ( ( A e. C /\ B e. D ) -> if ( ph , A , B ) e. _V ) $=
      ( vx vy wcel wa cv cif cvv wral visset ifex a1i rgen2 wceq ifeq1 eleq1d
      ifeq2 rcla42v mpi ) BDHCEHIAFJZGJZKZLHZGEMFDMABCKZLHZUGFGDEUGUDDHUEEHIAUD
      UEFNGNOPQUGUIABUEKZLHFGBCDEUDBRUFUJLAUDBUESTUECRUJUHLAUECBUATUBUC $.
      $( [21-Mar-2011] $)
  $}

  $( Multiplication by a number greater than or equal to 1.  (Contributed by
     Paul Chapman, 21-Mar-2011.) $)
  lemulge12 $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( 0 <_ A /\ 1 <_ B ) ) ->
                    A <_ ( B x. A ) ) $=
    ( cr wcel wa cc0 cle wbr c1 cmul co lemulge11 wb cc wceq mulcom recn
    syl2an breq2d adantr mpbid ) ACDZBCDZEZFAGHIBGHEZEAABJKZGHZABAJKZGHZABLUDUG
    UIMUEUDUFUHAGANDBNDUFUHOUBUCABPAQBQRSTUA $.
    $( [21-Mar-2011] $)

  $( The absolute value of a nonzero integer is a positive integer.
     (Contributed by Paul Chapman, 21-Mar-2011.)  (The proof was shortened by
     Andrew Salmon, 25-May-2011.) $)
  nnabscl $p |- ( ( N e. ZZ /\ N =/= 0 ) -> ( abs ` N ) e. NN ) $=
    ( cabs cfv cn wcel cz cc0 clt wbr wne wa elnnz cn0 nn0abscl nn0z syl
    adantr cc wb zcn absgt0 biimpa sylanbrc ) ABCZDEUDFEZGUDHIZAFEZAGJZKUDLUGUE
    UHUGUDMEUEANUDOPQUGUHUFUGAREUHUFSATAUAPUBUC $.
    $( [25-May-2011] $) $( [21-Mar-2011] $)

  $( A nonnegative integer is either ` 0 ` or positive.  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  elnn00nn $p |- ( N e. NN0 <-> ( N = 0 \/ N e. NN ) ) $=
    ( cn0 wcel cc0 wceq cn wo clt wbr cle nn0ge0 cr wb nn0re 0re leloe mpan
    syl mpbid elnnnn0b baib eqcom a1i orbi12d mpbird orcom sylib 0nn0 eleq1
    mpbiri nnnn0 jaoi impbii ) ABCZADEZAFCZGZUNUPUOGZUQUNURDAHIZDAEZGZUNDAJIZVA
    AKUNALCZVBVAMZANDLCVCVDODAPQRSUNUPUSUOUTUPUNUSATUAUOUTMUNADUBUCUDUEUPUOUFUG
    UOUNUPUOUNDBCUHADBUIUJAUKULUM $.
    $( [22-Jun-2011] $)

  $( Membership in a finite set of sequential integers.  (Contributed by Paul
     Chapman, 21-Mar-2011.) $)
  elfzm11 $p |- ( ( M e. ZZ /\ N e. ZZ ) -> ( K e. ( M ... ( N - 1 ) ) <->
                                         ( K e. ZZ /\ M <_ K /\ K < N ) ) ) $=
    ( cz wcel wa c1 cmin co cfz cle wbr w3a clt wb elfz1 peano2zm sylan2
    zltlem1 anbi2d expcom pm5.32d 3anass syl5bb syl6bbr adantl bitr4d ) BDEZCDE
    ZFABCGHIZJIEZADEZBAKLZAUJKLZMZULUMACNLZMZUHUJDEUKUOOUIABUJPCQRUIUQUOOUHUIUQ
    ULUMUNFZFZUOUIULUMUPFZFUSUQUIULUTURULUIUTUROULUIFUPUNUMACSTUAUBULUMUPUCUDUL
    UMUNUCUEUFUG $.
    $( [21-Mar-2011] $)

  $( An integer is a member of a 0-based finite set of sequential integers iff
     its successor is a member of the corresponding 1-based set.  (Contributed
     by Paul Chapman, 22-Jun-2011.) $)
  elfzp1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 0 ... ( N - 1 ) ) <-> ( K + 1 ) e. ( 1 ... N ) ) ) $=
    ( cz wcel wa c1 caddc co cfz cmin cc0 wb 1z fzsubel mpanl1 mpanr2 peano2z
    sylan2 ancoms cc wceq zcn ax1cn pncan mpan2 syl subidi opreq1i a1i eleq12d
    adantr bitr2d ) ACDZBCDZEAFGHZFBIHDZUOFJHZFFJHZBFJHZIHZDZAKUSIHZDZUNUMUPVAL
    ZUNUOCDZVDUMUNVEFCDZVDMVFUNVEVFEVDMUOFFBNOPAQRSUMVAVCLUNUMUQAUTVBUMATDZUQAU
    AZAUBVGFTDVHUCAFUDUEUFUTVBUAUMURKUSIFUCUGUHUIUJUKUL $.
    $( [22-Jun-2011] $)

  $( An integer is a member of a 1-based finite set of sequential integers iff
     its predecessor is a member of the corresponding 0-based set.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  elfzm1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 1 ... N ) <-> ( K - 1 ) e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel c1 cfz co cmin cc0 wb wa 1z fzsubel mpanl1 mpanr2 ax1cn subidi
    opreq1i eleq2i syl6bb ancoms ) BCDZACDZAEBFGDZAEHGZIBEHGZFGZDZJUBUCKUDUEEEH
    GZUFFGZDZUHUBUCECDZUDUKJZLULUBUCULKUMLAEEBMNOUJUGUEUIIUFFEPQRSTUA $.
    $( [22-Jun-2011] $)

  ${
    fz1eqblem.1 $e |- M e. NN0 $.
    fz1eqblem.2 $e |- N e. NN0 $.
    $( Lemma for ~ fz1eqb $. $)
    fz1eqblem $p |- ( ( 1 ... M ) = ( 1 ... N ) <-> M = N ) $=
      ( cc0 wceq c1 cfz co wb wa opreq2 eqcomd sylan9eq eqtr3 2thd wn c0 cn0
      wcel fz1n ax-mp biimpri eqeq1d biimpi eqcoms syl6bi con3d imp eqeq1
      eqcom syl6bb notbid biimpar con4bid eqeq2d impcom eqeq2 biimparc cn cuz
      cfv fzopth elnnuz syl2anb eqid biantrur syl6bbr wo elnn00nn mpbi ori
      syl2an 4cases ) AEFZBEFZGAHIZGBHIZFZABFZJZVOVPKVSVTVOVPVQGEHIZVRAEGHLVPVR
      WBBEGHLMNABEOPVOVPQZKZVSVTWDVSQZVTQZVOWCWEVOVSVPVOVSRVRFVPVOVQRVRVQRFZVOA
      STZWGVOJCAUAUBZUCUDVPVRRVRRFZVPBSTZWJVPJDBUAUBZUEUFUGUHUIVOWFWCVOVTVPVOVT
      EBFVPAEBUJEBUKULUMUNPUOVOQZVPKZVSVTWNWEWFVPWMWEVPVSVOVPVSWGVOVPVRRVQWJVPW
      LUCUPWGVOWIUEUGUHUQVPWFWMVPVTVOBEAURUMUSPUOAUTTZBUTTZWAWMWCWOWPKVSGGFZVTK
      ZVTAGVAVBZTBWSTVSWRJWOWPWSGBGAVCAVDBVDVEWQVTGVFVGVHVOWOWHVOWOVICAVJVKVLVP
      WPWKVPWPVIDBVJVKVLVMVN $.
      $( [22-Jun-2011] $)
  $}

  $( Two possibly-empty 1-based finite sets of sequential integers are equal
     iff their endpoints are equal.  (Contributed by Paul Chapman,
     22-Jun-2011.) $)
  fz1eqb $p |- ( ( M e. NN0 /\ N e. NN0 ) ->
                 ( ( 1 ... M ) = ( 1 ... N ) <-> M = N ) ) $=
    ( cn0 wcel c1 cfz co wceq wb cc0 cif opreq2 eqeq1d eqeq1 bibi12d eqeq2d
    eqeq2 0nn0 elimel fz1eqblem dedth2h ) ACDZBCDZEAFGZEBFGZHZABHZIEUBAJKZFGZUE
    HZUHBHZIUIEUCBJKZFGZHZUHULHZIABJJAUHHZUFUJUGUKUPUDUIUEAUHEFLMAUHBNOBULHZUJU
    NUKUOUQUEUMUIBULEFLPBULUHQOUHULAJCRSBJCRSTUA $.
    $( [22-Jun-2011] $)

  ${
    $d K x $.  $d M x y $.  $d N x y $.  $d ch x $.  $d ph y $.  $d ps x $.
    $d ta x $.  $d th x $.
    fzind.1 $e |- ( x = M -> ( ph <-> ps ) ) $.
    fzind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    fzind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    fzind.4 $e |- ( x = K -> ( ph <-> ta ) ) $.
    fzind.5 $e |- ( ( M e. ZZ /\ N e. ZZ /\ M <_ N ) -> ps ) $.
    fzind.6 $e |- ( ( ( M e. ZZ /\ N e. ZZ ) /\
                      ( y e. ZZ /\ M <_ y /\ y < N ) ) -> ( ch -> th ) ) $.
    $( Induction on the integers from ` M ` to ` N ` inclusive .  The first
       four hypotheses give us the substitution instances we need; the last two
       are the basis and the induction hypothesis.  (Contributed by Paul
       Chapman, 31-Mar-2011.) $)
    fzind $p |- ( ( ( M e. ZZ /\ N e. ZZ ) /\
                    ( K e. ZZ /\ M <_ K /\ K <_ N ) ) -> ta ) $=
      ( cz wcel cle wbr w3a wa wi cv c1 caddc co wceq breq1 anbi2d imbi12d
      3expb ex cr p1le 3expia zre syl2an imdistand imim1d 3ad2ant2 clt wb
      zltp1le adantlr expcom pm5.32d adantl 3expa com12 sylbird com23 exp3a
      3impib imp3a a2d syld uzind 3impia impcom ) HQRZIHSTZHJSTZUAZIQRZJQRZUBZE
      WDWEWFEWAWBWCWEWFEUCZUCWAWBUBZWEWCWHWEWIWCWHUCZWEWAWBWJWEWAWBUAZWFWCEWKWF
      WCEWFFUDZJSTZUBZAUCWFIJSTZUBZBUCWFGUDZJSTZUBZCUCZWFWQUEUFUGZJSTZUBZDUCZWF
      WCUBZEUCFGIHWLIUHZWNWPABXFWMWOWFWLIJSUIUJKUKWLWQUHZWNWSACXGWMWRWFWLWQJSUI
      UJLUKWLXAUHZWNXCADXHWMXBWFWLXAJSUIUJMUKWLHUHZWNXEAEXIWMWCWFWLHJSUIUJNUKWE
      WPBWEWFWOBOULUMWEWQQRZIWQSTZUAZWTXCCUCZXDXJWEWTXMUCXKXJXCWSCXJWFXBWRXJWFX
      BWRUCZWQUNRZJUNRZXNXJWFXOXPXBWRWQJUOUPWQUQJUQURUMUSUTVAXLXCCDXLWFXBCDUCZX
      LXBWFXQWEXJXKXBWFXQUCZUCWEXJXKUBZXBXRWEWFXSXBUBZXQWEWFXTXQUCWGXTXSWQJVBTZ
      UBZXQWFYBXTVCWEWFXSYAXBXSWFYAXBVCZXJWFYCXKWQJVDVEVFVGVHYBWGXQXJXKYAWGXQUC
      WGXJXKYAUAXQPVFVIVJVKUMVLVMVNVLVOVPVQVRVMVLULVFVLVSVOVT $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d K x $.  $d M x y $.  $d N x y $.  $d ch x $.  $d ph y $.  $d ps x $.
    $d ta x $.  $d th x $.
    fnn0ind.1 $e |- ( x = 0 -> ( ph <-> ps ) ) $.
    fnn0ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    fnn0ind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    fnn0ind.4 $e |- ( x = K -> ( ph <-> ta ) ) $.
    fnn0ind.5 $e |- ( N e. NN0 -> ps ) $.
    fnn0ind.6 $e |- ( ( N e. NN0 /\ y e. NN0 /\ y < N ) -> ( ch -> th ) ) $.
    $( Induction on the integers from ` 0 ` to ` N ` inclusive .  The first
       four hypotheses give us the substitution instances we need; the last two
       are the basis and the induction hypothesis.  (Contributed by Paul
       Chapman, 31-Mar-2011.) $)
    fnn0ind $p |- ( ( N e. NN0 /\ K e. NN0 /\ K <_ N ) -> ta ) $=
      ( cn0 wcel cle wbr wa cz cc0 wi w3a cr 0re letr mp3an1 zre syl2an ex
      com23 3impib elnn0z simplbi2 sylcom nn0z impbid1 0z sylbir 3adant1 cv
      clt lelttr ltle 3adant2 syld impcom 3expb anbi1i syl2anbr expcom 3impa
      exp3a mpd adantll fzind mpanl1 sylbird 3expa sylanb 3impb ) IPQZHPQZHIRSZ
      EWDWETWCEHUAQZUBHRSZTWEWCEUCZWDWFWGWEWHWFWGWEUDZWCIUAQZEWIWJWCWIWJUBIRSZW
      CWFWGWEWJWKUCZWFWJWGWETZWKWFWJWMWKUCZHUEQZIUEQZWNWFWJUBUEQZWOWPWNUFUBHIUG
      UHHUIIUIZUJUKULUMWCWJWKIUNZUOUPIUQURWJWIEUBUAQZWJWIEUSABCDEFGHUBIJKLMWJWK
      BWTWJWKTZWCBWSNUTVAWJGVBZUAQZUBXBRSZXBIVCSZUDZCDUCZWTWJXFTWKXGXFWJWKXCXDX
      EWLXCWJXDXETZWKXCWJXHWKUCZXBUEQZWPXIXCWJWQXJWPXIUFWQXJWPUDXHUBIVCSZWKUBXB
      IVDWQWPXKWKUCXJUBIVEVFVGUHXBUIWRUJUKULUMVHXFWJWKXGUCXFWJWKXGXCXDXEXAXGUCX
      AXCXDTZXETZXGWCXBPQZXETXGXAXMWCXNXEXGOVIWSXNXLXEXBUNVJVKVLVMVNVHVOVPVQVRV
      LVSVTHUNWAVHWB $.
      $( [31-Mar-2011] $)
  $}

  $( Absorbtion law for modulo.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  modaddabs $p |- ( ( A e. RR /\ B e. RR /\ C e. RR+ ) ->
            ( ( ( A mod C ) + ( B mod C ) ) mod C ) = ( ( A + B ) mod C ) ) $=
    ( cr wcel crp w3a cmo co caddc cc wceq wa modcl recn syl 3adant2 3adant1
    addcom syl11anc opreq1d simpl jca simpr modabs2 modadd1 syl111anc 3ad2ant2
    3simpc 3eqtr2rd eqtr4d ) ADEZBDEZCFEZGZACHIZBCHIZJIZCHIUQUPJIZCHIZABJICHIZU
    OURUSCHUOUPKEZUQKEZURUSLULUNVBUMULUNMZUPDEZVBACNZUPOPQZUMUNVCULUMUNMZUQDEZV
    CBCNZUQOPRUPUQSTUAUOUTBUPJIZCHIZUPBJIZCHIZVAUOVIUMMZVEUNMZUQCHIUQLZUTVLLUMU
    NVOULVHVIUMVJUMUNUBUCRULUNVPUMVDVEUNVFULUNUDUCQUMUNVQULBCUERUQBUPCUFUGUOVMV
    KCHUOVBBKEZVMVKLVGUMULVRUNBOUHUPBSTUAUOVEULMZVHUPCHIUPLZVNVALULUNVSUMVDVEUL
    VFULUNUBUCQULUMUNUIULUNVTUMACUEQUPABCUFUGUJUK $.
    $( [22-Jun-2011] $)

  $( An integer modulo 1 is 0.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  zmod10 $p |- ( N e. ZZ -> ( N mod 1 ) = 0 ) $=
    ( cz wcel c1 cmo co cfl cfv cmin cc0 cr wceq zre modfrac syl flid opreq2d
    cc zcn subid 3eqtrd ) ABCZADEFZAAGHZIFZAAIFZJUBAKCUCUELAMANOUBUDAAIAPQUBARC
    UFJLASATOUA $.
    $( [26-Jun-2011] $)

  $( Identity law for modulo restricted to integers.  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zmodid2 $p |- ( ( M e. ZZ /\ N e. NN ) -> ( ( M mod N ) = M <->
                                              M e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel cn wa cmo co wceq cc0 cle wbr clt c1 cmin cfz cr crp wb modid2
    zre nnrp syl2an nnz w3a 0z elfzm11 mpan 3anass syl6bb syl ibar bicomd
    sylan9bbr bitr4d ) ACDZBEDZFABGHAIZJAKLZABMLZFZAJBNOHPHDZAQDBRDURVASUPUQABT
    AUABUBUCUQVBUPVAFZUPVAUQBCDZVBVCSBUDVDVBUPUSUTUEZVCJCDVDVBVESUFAJBUGUHUPUSU
    TUIUJUKUPVAVCUPVAULUMUNUO $.
    $( [22-Jun-2011] $)

  $( Closure law for modulo restricted to integers.  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zmodfz $p |-
            ( ( M e. ZZ /\ N e. NN ) -> ( M mod N ) e. ( 0 ... ( N - 1 ) ) ) $=
    ( cz wcel cn wa cmo co cc0 c1 cmin cfz cle wbr clt w3a cn0 zmodcl nn0z syl
    cr crp modge0 zre nnrp syl2an modlt 3jca wb nnz 0z elfzm11 mpan adantl
    mpbird ) ACDZBEDZFZABGHZIBJKHLHDZUSCDZIUSMNZUSBONZPZURVAVBVCURUSQDVAABRUSST
    AUADZBUBDZVBUPUQABUCAUDZBUEZUFVEVFVCUPUQABUGVGVHUFUHUQUTVDUIZUPUQBCDZVIBUJI
    CDVJVIUKUSIBULUMTUNUO $.
    $( [22-Jun-2011] $)

  ${
    $d F k $.  $d K k $.  $d M k $.  $d N k $.
    seqzresval2.1 $e |- S e. _V $.
    seqzresval2.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seqzresval2 $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ( M ... N ) ) ->
                        ( ( <. M , S >. seq ( F |` ( M ... N ) ) ) ` K ) =
                        ( ( <. M , S >. seq F ) ` K ) ) $=
      ( vk cuz cfv wcel cfz co wa cv cres wceq wral cop cseqz elfzuzb biimpa
      simpld cz wss simprd eluzel2 adantr fzss2 syl11anc sseld fvres eqcomd
      syl6 r19.21aiv cvv resexg ax-mp seqzfveq ) EDIJZKZCDELMZKZNZCUTKZHOZBJZVF
      BVBPZJZQZHDCLMZRZCDASZVHTMJZCVMBTMJZQVDVEECIJKZVAVCVEVPNUTCDEUAUBZUCVDVJH
      VKVDVFVKKVFVBKZVJVDVKVBVFVDVPDUDKZVKVBUEVDVEVPVQUFVAVSVCDEUGUHCDEUIUJUKVR
      VIVGVFVBBULUMUNUOVEVLNVOVNAHBVHDCFGBUPKVHUPKGBVBUPUQURUSUMUJ $.
      $( [22-Jun-2011] $)
  $}

  ${
    seq1resval.1 $e |- S e. _V $.
    seq1resval.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq1 ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seq1resval $p |- ( N e. NN ->
        ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` N ) = ( ( S seq1 F ) ` N ) ) $=
      ( cn wcel c1 cop cfz co cres cseqz cfv wceq cseq1 cuz elnnuz seqzresval
      sylbi cvv resexg ax-mp seq1seqz fveq1i eqeq12i sylibr ) CFGZCHAIZBHCJKZLZ
      MKZNZCUIBMKZNZOZCAUKPKZNZCABPKZNZOUHCHQNGUPCRABHCDESTURUMUTUOCUQULAUKDBUA
      GUKUAGEBUJUAUBUCUDUECUSUNABDEUDUEUFUG $.
      $( [22-Jun-2011] $)
  $}

  ${
    seq1resval2.1 $e |- S e. _V $.
    seq1resval2.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq1 ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seq1resval2 $p |- ( ( N e. NN /\ K e. ( 1 ... N ) ) ->
        ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` K ) = ( ( S seq1 F ) ` K ) ) $=
      ( cn wcel c1 cfz co wa cop cres cseqz cfv wceq cseq1 cuz seqzresval2
      elnnuz sylanb cvv resexg ax-mp seq1seqz fveq1i eqeq12i sylibr ) DGHZCIDJK
      ZHZLCIAMZBUKNZOKZPZCUMBOKZPZQZCAUNRKZPZCABRKZPZQDISPHULUSUJABCIDEFTDUAUBV
      AUPVCURCUTUOAUNEBUCHUNUCHFBUKUCUDUEUFUGCVBUQABEFUFUGUHUI $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d A f k m s $.  $d F f s $.  $d K f m s $.  $d N f k m s $.  $d S f s $.
    $( Closure of the partial values of the 1-based recursive sequence builder
       on a finite sequence.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    fseq1cl $p |- ( ( ( A e. B /\ S : ( A X. A ) --> A ) /\
                  ( N e. ZZ /\ F : ( 1 ... N ) --> A /\ K e. ( 1 ... N ) ) ) ->
                    ( ( S seq1 F ) ` K ) e. A ) $=
      ( vf vs vm vk cz wcel c1 cfz co wf w3a cxp wa cseq1 cfv wi cvv oprex fex
      mpan2 xpexg anidms sylan2 anim12i 3impb 3comr cv wal 1z cle wbr elfz1
      caddc wceq fveq2 eleq1d imbi2d cn elnnz1 ffvelrn cuz elnnuz eluzfz1
      sylbi ancoms visset seq11 syl5eqel adantrr ex sylbir 3adant1 clt seq1p1
      adantr adantl 3ad2ant2 foprrn 3com23 cn0 nnnn0 cc0 elnn0z wb elfz mp3an2
      peano2z sylan cr zre 1re addge02 mpan syl zltp1le anbi12d bitr4d exbiri
      com23 expdimp imp32 syl3an2 eqeltrd 3expia anassrs expcom ancomsd
      sylanbr 3impa impcom adantll a2d fzind sylbid imp 19.21aivv feq1
      bi2anan9 opreq12 fveq1d imbi12d cla42gv mpan9 com12 exp3a mpdi imp3a
      3com12 ) FKLZMFNOZADPZEYPLZQABLZAARZACPZSZECDTOZUAZALZYQYOYRUUBUUEUBZYQYO
      YRUUFUUBYQYOYRSZSUUEUUBYQUUGUUEYSUUAYQUUGUUEUBZYSYQUUAUUHUUGYSYQUUAQZUUEU
      UGUUIDUCLZCUCLZSZUUEYQUUAYSUULYQUUAYSUULYQUUJUUAYSSUUKYQYPUCLUUJMFNUDYPAU
      CDUEUFUUAYTUCLZUUKYSYTAUCCUEYSUUMAABBUGUHUIUJUKULUUIUUGUULUUEUBUUIUUGUULU
      UEYQUUAUUGUULSZUUEUBYSUUNYQUUASZUUEUUGYPAGUMZPZYTAHUMZPZSZEUURUUPTOZUAZAL
      ZUBZHUNGUNUULUUOUUEUBZUUGUVDGHYOYRUVDMKLZYOYRUVDUBUOUVFYOSZYREKLMEUPUQEFU
      PUQQZUVDEMFURUVGUVHUVDUUTIUMZUVAUAZALZUBUUTMUVAUAZALZUBZUUTJUMZUVAUAZALZU
      BUUTUVOMUSOZUVAUAZALZUBUVDIJEMFUVIMUTZUVKUVMUUTUWAUVJUVLAUVIMUVAVAVBVCUVI
      UVOUTZUVKUVQUUTUWBUVJUVPAUVIUVOUVAVAVBVCUVIUVRUTZUVKUVTUUTUWCUVJUVSAUVIUV
      RUVAVAVBVCUVIEUTZUVKUVCUUTUWDUVJUVBAUVIEUVAVAVBVCYOMFUPUQZUVNUVFYOUWESFVD
      LZUVNFVEUWFUUTUVMUWFUUQUVMUUSUWFUUQSMUUPUAZAUVLUUQUWFUWGALZUUQMYPLZUWHUWF
      YPAMUUPVFUWFFMVGUALUWIFVHMFVIVJUIVKUURUUPHVLZGVLZVMVNVOVPVQVRUVGUVOKLZMUV
      OUPUQZUVOFVSUQZQZSUUTUVQUVTYOUWOUUTUVQUVTUBZUBZUVFUWOYOUWQUWLUWMUWNYOUWQU
      BUWLUWMSZUWNSYOUWQUWRUWNYOUWQUVOVDLZUWNYOSZUWQUWRUWSUWTSZUUSUUQUWPUUSUUQS
      UXAUWPUUSUUQUXAUWPUUSUUQUXASZUVQUVTUUSUXBUVQQUVSUVPUVRUUPUAZUUROZAUXBUUSU
      VSUXDUTZUVQUXAUXEUUQUWSUXEUWTUVOUURUUPUWJUWKVTWAWBWCUUSUXCALZUVQUXDALZUXB
      UUSUVQUXFUXGUVPUXCAAAUURWDWEUUQUVRYPLZUXFUXAYPAUVRUUPVFUWSUWNYOUXHUWSUVOW
      FLZUWNYOUXHUBZUBZUVOWGUXIUWLWHUVOUPUQZSUXKUVOWIUWLUXLUWNUXJUWLYOUXLUWNSZU
      XHUWLYOUXHUXMUWLYOSZUXHMUVRUPUQZUVRFUPUQZSZUXMUVRKLZYOUXHUXQWJZUWLUXRUVFY
      OUXSUOUVRMFWKWLUVOWMWNUXNUXLUXOUWNUXPUWLUXLUXOWJZYOUWLUVOWOLZUXTUVOWPMWOL
      UYAUXTWQMUVOWRWSWTWAUVOFXAXBXCXDXEXFVJWTXGUIXHXIXJXKXLXMUVOVEXNXKVPXOXPXQ
      XRXSVPXTWSYAYBUVDUVEGHDCUCUCUUPDUTZUURCUTZSZUUTUUOUVCUUEUYBUUQYQUYCUUSUUA
      YPAUUPDYCYTAUURCYCYDUYDUVBUUDAUYDEUVAUUCUYCUYBUVAUUCUTUURCUUPDTYEVKYFVBYG
      YHYIYJVRYKYJYLYJWEXJYMYJUKYNXP $.
      $( [26-Jun-2011] $)
  $}

  ${
    seq0cl.1 $e |- S e. _V $.
    seq0cl.2 $e |- F e. _V $.
    $( Closure of the value of the 0-based recursive sequence builder.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    seq0cl $p |- ( ( N e. NN0 /\ F : NN0 --> C /\ S : ( C X. C ) --> C ) ->
                   ( ( S seq0 F ) ` N ) e. C ) $=
      ( cn0 wcel wf cxp w3a cc0 cop cseqz co cfv cseq0 cuz seqzcl elnn0uz
      syl3an1b nn0uz feq2i syl3an2b seq0seqz fveq1i syl5eqel ) DGHZGACIZAAJABIZ
      KDLBMCNOZPZADBCQOZPUHLRPZACIZUJULAHZUIDUNHUOUJUPUHABCLDEFSDTUAGUNACUBUCUD
      DUMUKBCEFUEUFUG $.
      $( [31-Mar-2011] $)
  $}

