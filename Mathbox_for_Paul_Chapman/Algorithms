$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Algorithms
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A z $.  $d R z $.  $d S z $.
    algrf.1 $e |- S e. _V $.
    algrf.2 $e |- F : S --> S $.
    algrf.3 $e |-
               R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    $( An algorithm is step a function ` F : S --> S ` on a state space
       ` S ` .  An algorithm acts on an initial state ` A e. S ` by iteratively
       applying ` F ` to give ` A ` , ` ( F `` A ) ` , ` ( F `` ( F `` A ) ) `
       and so on.  An algorithm is said to halt if a fixed point of ` F ` is
       reached after a finite number of iterations.

       The algorithm iterator ` R : NN0 --> S ` "runs" the algorithm ` F ` so
       that ` ( R `` k ) ` is the state after ` k ` iterations of ` F ` on the
       initial state ` A ` .

       Domain and codomain of the algorithm iterator ` R ` .  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algrf $p |- ( A e. S -> R : NN0 --> S ) $=
      ( vz wcel cv cfv cn0 wral wf wa c1st cxp cres ccom csn cseq0 co f1stres
      fco mp2an cvv xpex fex nn0ex snex seq0cl mp3an3 wss fss fconstg snssi
      sylanc sylan2 fveq1i syl5eqel ancoms r19.21aiva wfn ffnfv seq0fn fneq1i
      mpbir mpbiran sylibr ) ACIZHJZBKZCIZHLMZLCBNZVJVMHLVKLIZVJVMVPVJOVKDPCCQZ
      RZSZLATZQZUAUBZKZCVLVPLCWANZWCCIZVJVPWDVQCVSNZWECCDNVQCVRNWFFCCUCVQCCDVRU
      DUEZCVSWAVKWFVQUFIVSUFIWGCCEEUGVQCUFVSUHUEZLVTUIAUJUGZUKULLVTWANVTCUMWDVJ
      LVTCWAUNLACUOACUPUQURVKBWBGUSUTVAVBVOBLVCZVNHLCBVDWJWBLVCVSWAWHWIVELBWBGV
      FVGVHVI $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)

    $( The value of the algorithm iterator ` R ` at ` 0 ` is the initial state
       ` A ` .  (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algr0 $p |- ( A e. S -> ( R ` 0 ) = A ) $=
      ( wcel cc0 cn0 csn cxp cfv wceq 0nn0 fvconst2g mpan2 c1st cres ccom
      cseq0 co fveq1i wf cvv f1stres fco mp2an xpex fex nn0ex snex seq00 eqtri
      syl5eq ) ACHZIJAKZLZMZAIBMZUPIJHUSANOJAICPQUTIDRCCLZSZTZURUAUBZMUSIBVDGUC
      VCURVACVCUDZVAUEHVCUEHCCDUDVACVBUDVEFCCUFVACCDVBUGUHCCEEUIVACUEVCUJUHJUQU
      KAULUIUMUNUO $.
      $( [31-Mar-2011] $)
  $}

  ${
    algrp1lem.1 $e |- Fun F $.
    $( Lemma for ~ algrp1 . $)
    algrp1lem $p |- ( ( A e. S /\ B e. S /\ K e. NN0 ) ->
 ( B ( F o. ( 1st |` ( S X. S ) ) ) ( ( NN0 X. { A } ) ` K ) ) = ( F ` B ) ) $=
      ( wcel cn0 w3a csn cxp cfv cop c1st cres ccom co wceq wa opelxpi
      fvconst2g eleq1d biimprd adantrd pm2.43i sylan2 3impb 3com12 wfun wf
      f1stres fvco3 mp3an12 syl fvres op1stg 3ad2ant2 eqtrd fveq2d df-opr
      syl5eq ) ACGZBCGZEHGZIZBEHAJKLZMZDNCCKZOZPZLZBDLZBVFVJQVEVKVGVILZDLZVLVEV
      GVHGZVKVNRZVCVBVDVOVCVBVDVOVCVFCGZVOVBVDSZBVFCCTVRVQVRVBVQVDVRVQVBVRVFACH
      AECUAUBUCUDUEUFUGUHZDUIVHCVIUJVOVPFCCUKVHCVGDVIULUMUNVEVMBDVEVMVGNLZBVEVO
      VMVTRVSVGVHNUOUNVCVBVTBRVDBVFCUPUQURUSURBVFVJUTVA $.
      $( [31-Mar-2011] $)
  $}

  ${
    algrp1.1 $e |- S e. _V $.
    algrp1.2 $e |- F : S --> S $.
    algrp1.3 $e |-
                R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    $( The value of the algorithm iterator ` R ` at ` ( K + 1 ) ` .
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    algrp1 $p |- ( ( A e. S /\ K e. NN0 ) ->
                   ( R ` ( K + 1 ) ) = ( F ` ( R ` K ) ) ) $=
      ( wcel cn0 wa c1 caddc co cfv csn cxp c1st cres ccom wceq cseq0 wf cvv
      f1stres fco mp2an xpex fex nn0ex snex seq0p1 fveq1i opreq1i eqeq12i
      sylibr adantl wfun ffun ax-mp algrp1lem pm3.26 ffvelrn algrf sylan
      peano2nn0 syl3anc eqtrd ) ACIZEJIZKZELMNZBOZEBOZVLJAPZQZOZDRCCQZSZTZNZVND
      OZVJVMWAUAZVIVJVLVTVPUBNZOZEWDOZVQVTNZUAWCVTVPEVRCVTUCZVRUDIVTUDICCDUCZVR
      CVSUCWHGCCUEVRCCDVSUFUGCCFFUHVRCUDVTUIUGJVOUJAUKUHULVMWEWAWGVLBWDHUMVNWFV
      QVTEBWDHUMUNUOUPUQVIVNCIZVLJIZWAWBUAVKAVNCDVLWIDURGCCDUSUTVAVIVJVBJCBUCVJ
      WJVIJCEBVCABCDFGHVDVEVJWKVIEVFUQVGVH $.
      $( [31-Mar-2011] $)

    $d A k m $.  $d F x $.  $d I k m x $.  $d K m $.  $d R k m x $.
    $d S k m x $.
    alginv.4 $e |- I Fn S $.
    alginv.5 $e |- ( x e. S -> ( I ` ( F ` x ) ) = ( I ` x ) ) $.
    $( If ` I ` is an invariant of ` F ` , its value is unchanged after any
       number of iterations of ` F ` .  (Contributed by Paul Chapman,
       31-Mar-2011.) $)
    alginv $p |- ( ( A e. S /\ K e. NN0 ) ->
                   ( I ` ( R ` K ) ) = ( I ` ( R ` 0 ) ) ) $=
      ( vm vk cn0 wcel cfv cc0 wceq cv wi c1 caddc co fveq2 fveq2d eqeq1d
      imbi2d eqidd wa algrp1 wf ffvelrn algrf sylan eqeq12d rgen vtoclri syl
      eqtrd biimprd expcom a2d nn0ind impcom ) GOPBDPZGCQZFQZRCQZFQZSZVFMTZCQZF
      QZVJSZUAVFVJVJSZUAVFNTZCQZFQZVJSZUAVFVQUBUCUDZCQZFQZVJSZUAVFVKUAMNGVLRSZV
      OVPVFWEVNVJVJWEVMVIFVLRCUEUFUGUHVLVQSZVOVTVFWFVNVSVJWFVMVRFVLVQCUEUFUGUHV
      LWASZVOWDVFWGVNWCVJWGVMWBFVLWACUEUFUGUHVLGSZVOVKVFWHVNVHVJWHVMVGFVLGCUEUF
      UGUHVFVJUIVQOPZVFVTWDVFWIVTWDUAVFWIUJZWDVTWJWCVSVJWJWCVREQZFQZVSWJWBWKFBC
      DEVQHIJUKUFWJVRDPZWLVSSZODCULWIWMVFODVQCUMBCDEHIJUNUOATZEQZFQZWOFQZSZWNAV
      RDWOVRSZWQWLWRVSWTWPWKFWOVREUEUFWOVRFUEUPWSADLUQURUSUTUGVAVBVCVDVE $.
      $( [22-Jun-2011] $) $( [31-Mar-2011] $)
  $}

  ${
    $d A k $.  $d C k z $.  $d F z $.  $d N k $.  $d R k z $.  $d S k z $.
    algcvg.1 $e |- S e. _V $.
    algcvg.2 $e |- F : S --> S $.
    algcvg.3 $e |-
             R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    algcvg.4 $e |- C : S --> NN0 $.
    algcvg.5 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                 ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvg.6 $e |- N = ( C ` A ) $.
    $( One way to prove that an algorithm halts is to construct a countdown
       function ` C : S --> NN0 ` whose value is guaranteed to decrease for
       each iteration of ` F ` until it reaches ` 0 ` .  That is, if ` X e. S `
       is not a fixed point of ` F ` , then
       ` ( C `` ( F `` X ) ) < ( C `` X ) ` .

       If ` C ` is a countdown function for algorithm ` F ` , the sequence
       ` ( C `` ( R `` k ) ) ` reaches ` 0 ` after at most ` N ` steps, where
       ` N ` is the value of ` C ` for the initial state ` A ` .  (Contributed
       by Paul Chapman, 22-Jun-2011.) $)
    algcvg $p |- ( A e. S -> ( C ` ( R ` N ) ) = 0 ) $=
      ( vk wcel ccom cfv cc0 cn0 wf wceq wfun ffun ax-mp fvco3 mp3an1 algrf
      ffvelrni syl5eqel sylanc fco mpan syl 0nn0 mp3an13 algr0 fveq2d eqtrd
      syl6reqr cv wa c1 caddc co wne clt wbr wi ffvelrn sylan fveq2 neeq1d
      breq12d imbi12d rgen vtoclri peano2nn0 syl2an algrp1 mpbird nn0seqcvgd
      eqtr3d ) BEOZGCDPZQZGDQCQZRSEDTZGSOZWEWFUAZWCCUBZWGWHWIESCTZWJKESCUCUDZSE
      GCDUEUFBDEFHIJUGZWCBCQZSGESBCKUHMUIUJWCNWDGWCWGSSWDTZWMWKWGWOKSESCDUKULUM
      WCRWDQZWNGWCWPRDQZCQZWNWCWGWPWRUAZWMWJWGRSOWSWLUNSERCDUEUOUMWCWQBCBDEFHIJ
      UPUQURMUSWCNUTZSOZVAZWTVBVCVDZWDQZRVEZXDWTWDQZVFVGZVHWTDQZFQZCQZRVEZXJXHC
      QZVFVGZVHZXBXHEOZXNWGXAXOWCSEWTDVIWMVJAUTZFQZCQZRVEZXRXPCQZVFVGZVHZXNAXHE
      XPXHUAZXSXKYAXMYCXRXJRYCXQXICXPXHFVKUQZVLYCXRXJXTXLVFYDXPXHCVKVMVNYBAELVO
      VPUMXBXEXKXGXMXBXDXJRXBXDXCDQZCQZXJWGXCSOZXDYFUAZWCXAWJWGYGYHWLSEXCCDUEUF
      WMWTVQVRXBYEXICBDEFWTHIJVSUQURZVLXBXDXJXFXLVFYIWGXAXFXLUAZWCWJWGXAYJWLSEW
      TCDUEUFWMVJVMVNVTWAWB $.
      $( [22-Jun-2011] $)
  $}

  $( Lemma for ~ algcvgb . $)
  algcvgblem $p |- ( ( M e. NN0 /\ N e. NN0 ) -> ( ( N =/= 0 -> N < M ) <->
                      ( ( M =/= 0 -> N < M ) /\ ( M = 0 -> N = 0 ) ) ) ) $=
    ( cn0 wcel wa cc0 wne clt wbr wi wceq wn wo cle cr wb pm3.26 nn0re syl 0re
    ltnle mpan nn0le0eq0 notbid adantr bitrd df-ne syl6bbr anbi2d nne breq1
    sylbi biimpar a1i sylbird exp3a ax-1 jctir jaob sylibr imor syl5ib nn0ge0
    adantl lelttr mp3an1 pm3.27 sylanc mpand sylibd pm3.33 mpan2d jcad ancoms
    impbid con34b imbi12i bitr4i anbi2i ) ACDZBCDZEZBFGZBAHIZJZAFGZWDJZWCWFJZEZ
    WGAFKZBFKZJZEWBWEWIWBWEWGWHWBWCLZWDMZWGWEWBWMWGJZWDWGJZEWNWGJWBWOWPWBWMWFWD
    WBWMWFEWMFAHIZEZWDWBWQWFWMWBWQWJLZWFWBWQAFNIZLZWSWBAODZWQXAPZWBVTXBVTWAQARS
    ZFODZXBXCTFAUAUBSVTXAWSPWAVTWTWJAUCUDUEUFAFUGZUHZUIWRWDJWBWMWDWQWMWKWDWQPBF
    UJBFAHUKULUMUNUOUPWDWFUQURWMWGWDUSUTWCWDVAVBWBWEWDWFJZWHWBWDWQWFWBFBNIZWDWQ
    WAXIVTBVCVDBODZXBXIWDEWQJZWBXEXJXBXKTFBAVEVFWBWAXJVTWAVGBRSXDVHVIXGVJWEXHEW
    HJWBWCWDWFVKUNVLVMWIWEJWBWHWGWEWCWFWDVKVNUNVOWLWHWGWLWKLZWSJWHWJWKVPWCXLWFW
    SBFUGXFVQVRVSUH $.
    $( [31-Mar-2011] $)

  ${
    algcvgb.1 $e |- F : S --> S $.
    algcvgb.2 $e |- C : S --> NN0 $.
    $( Two ways of expressing that ` C ` is a countdown function for algorithm
       ` F ` .  The first is used in these theorems.  The second states the
       condition more intuitively as a conjunction: if the countdown function's
       value is currently non-zero, it must decrease at the next step; if it
       has reached zero, it must remain zero at the next step.  (Contributed by
       Paul Chapman, 31-Mar-2011.) $)
    algcvgb $p |- ( X e. S ->
        ( ( ( C ` ( F ` X ) ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) <->
          ( ( ( C ` X ) =/= 0 -> ( C ` ( F ` X ) ) < ( C ` X ) ) /\
            ( ( C ` X ) = 0 -> ( C ` ( F ` X ) ) = 0 ) ) ) ) $=
      ( cfv cn0 wcel cc0 wne clt wbr wi wceq wa wb algcvgblem ffvelrni syl
      sylanc ) DAGZHIDCGZAGZHIZUDJKUDUBLMZNUBJKUFNUBJOUDJONPQDBIZUBUDRBHDAFSUGU
      CBIUEBBDCESBHUCAFSTUA $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d A k m $.  $d C k m z $.  $d F z $.  $d K m $.  $d N k m $.
    $d R k m z $.  $d S k m z $.
    algcvga.1 $e |- S e. _V $.
    algcvga.2 $e |- F : S --> S $.
    algcvga.3 $e |-
              R = ( ( F o. ( 1st |` ( S X. S ) ) ) seq0 ( NN0 X. { A } ) ) $.
    algcvga.4 $e |- C : S --> NN0 $.
    algcvga.5 $e |- ( z e. S -> ( ( C ` ( F ` z ) ) =/= 0 ->
                                  ( C ` ( F ` z ) ) < ( C ` z ) ) ) $.
    algcvga.6 $e |- N = ( C ` A ) $.
    $( The countdown function ` C ` remains ` 0 ` after ` N ` steps.
       (Contributed by Paul Chapman, 22-Jun-2011.) $)
    algcvga $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) ->
                                ( C ` ( R ` K ) ) = 0 ) ) $=
      ( vm vk wcel cn0 cuz cfv cc0 wceq wi ffvelrni syl5eqel cz nn0z cle wbr
      wa eluz1 cv c1 caddc co fveq2 fveq2d eqeq1d imbi2d algcvg a1i w3a nn0ge0
      adantr cr 0re letr mp3an1 nn0re zre syl2an mpand elnn0z pm3.26bi2 adantl
      syld sylan 3impia 3expb expcom 3adant1 ancld wf ffvelrn algrf wne clt
      neeq1d breq12d imbi12d rgen vtoclri algcvgb pm3.27 syl6bi mpd syl algrp1
      sylibrd syl6 a2d uzind 3expib sylbid com3r ) BEQZHRQZGHSTQZGDTZCTZUAUBZUC
      XFBCTRHERBCLUDNUEZXGXHXFXKXGHUFQZXHXFXKUCZUCHUGXMXHGUFQZHGUHUIZUJXNHGUKXM
      XOXPXNXFOULZDTZCTZUAUBZUCXFHDTZCTZUAUBZUCZXFPULZDTZCTZUAUBZUCXFYEUMUNUOZD
      TZCTZUAUBZUCXNOPHGXQHUBZXTYCXFYMXSYBUAYMXRYACXQHDUPUQURUSXQYEUBZXTYHXFYNX
      SYGUAYNXRYFCXQYEDUPUQURUSXQYIUBZXTYLXFYOXSYKUAYOXRYJCXQYIDUPUQURUSXQGUBZX
      TXKXFYPXSXJUAYPXRXICXQGDUPUQURUSYDXMABCDEFHIJKLMNUTVAXMYEUFQZHYEUHUIZVBZX
      FYHYLYSXFXFYERQZUJZYHYLUCYSXFYTYQYRXFYTUCXMXFYQYRUJYTXFYQYRYTXFYQYRYTXGYQ
      YRYTUCXFXGYQUJZYRUAYEUHUIZYTUUBUAHUHUIZYRUUCXGUUDYQHVCVDHVEQZYEVEQZUUDYRU
      JUUCUCZXGYQUAVEQUUEUUFUUGVFUAHYEVGVHHVIYEVJVKVLYQUUCYTUCXGYTYQUUCYEVMVNVO
      VPXLVQVRVSVTWAWBUUAYHYFFTZCTZUAUBZYLUUAYFEQZYHUUJUCZREDWCYTUUKXFREYEDWDBD
      EFIJKWEVQUUKUUIUAWFZUUIYGWGUIZUCZUULAULZFTZCTZUAWFZUURUUPCTZWGUIZUCZUUOAY
      FEUUPYFUBZUUSUUMUVAUUNUVCUURUUIUAUVCUUQUUHCUUPYFFUPUQZWHUVCUURUUIUUTYGWGU
      VDUUPYFCUPWIWJUVBAEMWKWLUUKUUOYGUAWFUUNUCZUULUJUULCEFYFJLWMUVEUULWNWOWPWQ
      UUAYKUUIUAUUAYJUUHCBDEFYEIJKWRUQURWSWTXAXBXCXDWQXEWP $.
      $( [22-Jun-2011] $)

    $d K z $.  $d N z $.
    algfx.7 $e |- ( z e. S -> ( ( C ` z ) = 0 -> ( F ` z ) = z ) ) $.
    $( If ` F ` reaches a fixed point when the countdown function ` C ` reaches
       ` 0 ` , ` F ` remains fixed after ` N ` steps.  (Contributed by Paul
       Chapman, 22-Jun-2011.) $)
    algfx $p |- ( A e. S -> ( K e. ( ZZ>= ` N ) -> ( R ` K ) = ( R ` N ) ) ) $=
      ( vm vk wcel cz cuz cfv wceq wi cn0 ffvelrni syl5eqel nn0z syl wa cv cle
      wbr crab uzval eleq2d pm5.32i c1 caddc co fveq2 eqeq1d imbi2d eqidd a1i
      algrp1 pm3.26 eluznn0 sylan sylanc cc0 algcvga imp jca wf ffvelrn algrf
      id eqeq12d imbi12d rgen vtoclri 3syl mpd eqtrd biimprd expcom adantl
      sylbir a2d uzind3 sylbi ex com3r ) BERZHSRZGHTUAZRZGDUAZHDUAZUBZUCWNHUDRZ
      WOWNBCUAUDHEUDBCLUENUFZHUGUHWOWQWNWTWOWQWNWTUCZWOWQUIWOGHAUJZUKULASUMZRZU
      IXCWOWQXFWOWPXEGAHUNZUOUPWNPUJZDUAZWSUBZUCWNWSWSUBZUCZWNQUJZDUAZWSUBZUCWN
      XMUQURUSZDUAZWSUBZUCXCPAQHGXHHUBZXJXKWNXSXIWSWSXHHDUTVAVBXHXMUBZXJXOWNXTX
      IXNWSXHXMDUTVAVBXHXPUBZXJXRWNYAXIXQWSXHXPDUTVAVBXHGUBZXJWTWNYBXIWRWSXHGDU
      TVAVBXLWOWNWSVCVDWOXMXERZUIZWNXOXRYDWOXMWPRZUIWNXOXRUCZUCZWOYEYCWOWPXEXMX
      GUOUPYEYGWOWNYEYFWNYEUIZXRXOYHXQXNWSYHXQXNFUAZXNWNXMUDRZXQYIUBYHBDEFXMIJK
      VEWNYEVFZXAYEYJWNXMHVGXBVHZVIYHXNCUAZVJUBZYIXNUBZWNYEYNABCDEFXMHIJKLMNVKV
      LYHWNYJUIXNERZYNYOUCZYHWNYJYKYLVMUDEDVNYJYPWNUDEXMDVOBDEFIJKVPVHXDCUAZVJU
      BZXDFUAZXDUBZUCZYQAXNEXDXNUBZYSYNUUAYOUUCYRYMVJXDXNCUTVAUUCYTYIXDXNXDXNFU
      TUUCVQVRVSUUBAEOVTWAWBWCWDVAWEWFWGWHWIWJWKWLWMWC $.
      $( [22-Jun-2011] $)
  $}

