$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Symmetry groups and Cayley's Theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( The symmetry group on ` A ` is a group.  (Contributed by Paul Chapman,
       25-Feb-2008.) $)
    symggrp $p |- ( A e. B -> ( SymGrp ` A ) e. Grp ) $=
      ( vx cv csymgrp cfv cgr wcel wceq fveq2 eleq1d visset symggrpi vtoclg )
      CDZEFZGHAEFZGHCABOAIPQGOAEJKOCLMN $.
      $( [25-Feb-2008] $)
  $}

  ${
    $d A a b f g h $.  $d F f w x y z $.  $d G a b c f g h w x y z $.
    $d H w x y z $.  $d P c g h $.  $d S y $.  $d U a b y $.
    $d X a b c f g h w x y z $.  $d Y y $.
    cayleylem1.1 $e |- G e. Grp $.
    cayleylem1.2 $e |- P = { f | f : X -1-1-onto-> X } $.
    cayleylem1.3 $e |- X = ran G $.
    cayleylem1.4 $e |- U = ( Id ` G ) $.
    cayleylem1.5 $e |- H = ( SymGrp ` X ) $.
    cayleylem1.6 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                             ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleylem1.7 $e |- Y = ran F $.
    cayleylem1.8 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Lemma for ~ cayleyi . $)
    cayleylem1 $p |- ( A e. X -> ( F ` A ) e. P ) $=
      ( wcel cfv wf1o cgr grplactf1o mpan crn cvv rnexg ax-mp eqeltri elsymgrn
      sylibr ) AKUCZKKAHUDZUEZUQBUCIUFUCZUPUROAFGHIKMNTQUGUHEKBUQKIUIZUJQUSUTUJ
      UCOIUFUKULUMPUNUO $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem2 $p |- F e. ( G GrpHom H ) $=
      ( vx vy vc vz vw cghom co wcel wf cv cfv wceq wral cgr wa wb csymgrp crn
      cvv rnexg ax-mp eqeltri symggrpi cxp symgf fdmi grprn rneqi eqtr4i
      elghom mp2an copab grplactfval mpan cayleylem1 eqeltrrd rgen opreq1
      eqeq2d anbi2d opabbidv eleq1d cbvralv fopab2 bitri mpbi ccom symgoprv
      syl2an opreqi syl5eq w3a grpass grplactval mp3an1 grpcl sylan 3impa wfun
      wfn wi fvco2 3expia wf1o elsymgrn sylib f1of ffn 3syl fnfun syl 3impia
      fveq2d 3adant1 sylan2 3impb 3eqtrd 3eqtr4rd r19.21aiv eqfnfv2 wss fnco
      frn syl3an 3anidm23 sylanc biimprd mpd eqtrd rgen2a fveq2 opreq1d
      eqeq12d opreq2d opreq2 cbvral2v mpbir2an ) GHIUGUHUIZJAGUJZUBUKZGULZUCUKZ
      GULZIUHZUUAUUCHUHZGULZUMZUCJUNUBJUNZHUOUIZIUOUIYSYTUUIUPUQNIJURULZUORJJHU
      SZUTPUUJUULUTUINHUOVAVBVCZVDZVCUBUCGHIAJPAUUKUSIUSUUKAUUNAAVEAUUKDJAUUMOV
      FVGVHIUUKRVIVJVKVLLUKZJUIZMUKZUDUKZUUOHUHZUMZUPZLMVMZAUIZUDJUNZYTUVCUDJUU
      RJUIZUURGULZUVBAUUJUVEUVFUVBUMNUUREFGHJLMSPVNVOUURABCDEFGHIJKLMNOPQRSTUAV
      PVQVRUVDUUPUUQEUKZUUOHUHZUMZUPZLMVMZAUIZEJUNYTUVCUVLUDEJUURUVGUMZUVBUVKAU
      VMUVAUVJLMUVMUUTUVIUUPUVMUUSUVHUUQUURUVGUUOHVSVTWAWBWCWDEFJAUVKGSWEWFWGUE
      UKZGULZUFUKZGULZIUHZUVNUVPHUHZGULZUMZUFJUNUEJUNUUIUWAUEUFJUVNJUIZUVPJUIZU
      PZUVRUVOUVQWHZUVTUWDUVOUVQUUKUHZUWEUVRUVOAUIZUVQAUIZUWFUWEUMUWBUWCDJAUVOU
      VQUUMOWIUVNABCDEFGHIJKLMNOPQRSTUAVPZUVPABCDEFGHIJKLMNOPQRSTUAVPZWJIUUKUVO
      UVQRWKWLUWDUUAUWEULZUUAUVTULZUMZUBJUNZUWEUVTUMZUWDUWMUBJUWBUWCUUAJUIZUWMU
      WBUWCUWPWMZUVSUUAHUHZUVNUVPUUAHUHZHUHZUWLUWKUUJUWQUWRUWTUMNUVNUVPUUAHJPWN
      VOUWBUWCUWPUWLUWRUMZUVSJUIZUWPUXAUWDUUJUXBUWPUXANUVSUUAEFGHJLMSPWOWPUUJUW
      BUWCUXBNUVNUVPHJPWQWPZWRWSUWQUWKUUAUVQULZUVOULZUWSUVOULZUWTUWBUWCUWPUWKUX
      EUMZUVOWTZUVQJXAZUWPUXGXBUWBUWCUXHUXIUWPUXGJUUAUVOUVQXCXDUWBUVOJXAZUXHUWB
      JJUVOXEZJJUVOUJUXJUWBUWGUXKUWIDJAUVOUUMOXFXGJJUVOXHJJUVOXIXJZJUVOXKXLUWCJ
      JUVQXEZJJUVQUJZUXIUWCUWHUXMUWJDJAUVQUUMOXFXGZJJUVQXHZJJUVQXIXJZWJXMUWCUWP
      UXEUXFUMUWBUWCUWPUPZUXDUWSUVOUUJUWCUWPUXDUWSUMNUVPUUAEFGHJLMSPWOWPXNXOUWB
      UWCUWPUXFUWTUMZUWBUWSJUIZUXSUXRUUJUWBUXTUXSNUVNUWSEFGHJLMSPWOWPUUJUWCUWPU
      XTNUVPUUAHJPWQWPXPXQXRXSXDXTUWDUWOUWNUWEJXAZUVTJXAZUWOUWNUQUWDUBJUWEUVTYA
      UWBUWCUYAUXJUXIUVQUSJYBZUYAUWBUWCUWCJJUVOUVQYCUXLUXQUWCUXMUXNUYCUXOUXPJJU
      VQYDXJYEYFUWDUXBUYBUXCUXBJJUVTXEZJJUVTUJUYBUXBUVTAUIUYDUVSABCDEFGHIJKLMNO
      PQRSTUAVPDJAUVTUUMOXFXGJJUVTXHJJUVTXIXJXLYGYHYIYJYKUWAUUHUUBUVQIUHZUUAUVP
      HUHZGULZUMUEUFUBUCJJUVNUUAUMZUVRUYEUVTUYGUYHUVOUUBUVQIUVNUUAGYLYMUYHUVSUY
      FGUVNUUAUVPHVSXNYNUVPUUCUMZUYEUUEUYGUUGUYIUVQUUDUUBIUVPUUCGYLYOUYIUYFUUFG
      UVPUUCUUAHYPXNYNYQWGYR $.
      $( [25-Jul-2008] $) $( [3-Mar-2008] $)

    $( Lemma for ~ cayleyi . $)
    cayleylem3 $p |- ( S e. ( SubGrp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vy csubg cfv wcel cgiso co csymgrp cgr crn cvv rnexg ax-mp eqeltri
      symggrpi cayleylem2 ghomgrpi cghom wf1o wa wb wss w3a issubg mpbi
      3simp2i elgiso mp2an ghomgsg mp3an cv cgi wceq wi wral cxp cres wfn wfo
      eqid ghomfo forn syl eqtri grpfo fofn fnresdm eqcomi ghomf1o grpidcl
      grplactval mp3an13 grprid mpan eqtrd eqeq1d cid subgid fveq2i symgidi
      eqtr2i eqeq2i fveq1 fvresi syl6eq sylbir syl5bi mprgbir f1oeq3 f1oeq2
      bitri mpbir2an pm3.2i ) BIUCUDUEZGHBUFUGUEZBGHIKNIJUHUDZUIRJJHUJZUKPHUIUE
      ZXQUKUENHUIULUMUNZUOUNZABCDEFGHIJKLMNOPQRSTUAUPZTUAUQZXOGHBURUGUEZXQBUJZG
      USZXRBUIUEZXOYCYEUTVANIUIUEZYFBIVBZXNYGYFYHVCYBIBVDVEVFZGHBVGVHXRYGGHIURU
      GUEZYCNXTYABGHIKTUAVIVJZJKGUSZYEYLUBVKZGUDZBVLUDZVMZYMCVMZVNZUBJXRYFYCYLY
      RUBJVOVANYIYKUBBYOCGHBJKKPTBKKVPZVQZBYFYTBVMZYIYFBYSVRZUUAYFYSKBVSUUBBKKG
      UJZYDTXRYGYJUUCYDVMZNXTYAXRYGYJVCJYDGVSUUDBGHIJKYDPTUAYDVTWAJYDGWBWCVJWDZ
      WEYSKBWFWCYSBWGWCUMWHUUEQYOVTZWIVJYMJUEZCYNUDZCVMZYQYPUUGUUHYMCUUGUUHYMCH
      UGZYMXRUUGCJUEZUUHUUJVMNXRUUKNCHJPQWJUMZYMCEFGHJLMSPWKWLXRUUGUUJYMVMNYMCH
      JPQWMWNWOWPYPYNWQJVQZVMZUUIUUMYOYNYOIVLUDZUUMXNYOUUOVMYBYOUUOIBUUOVTUUFWR
      UMUUOXPVLUDUUMIXPVLRWSJXSWTWDXAXBUUNUUHCUUMUDZCCYNUUMXCUUKUUPCVMUULJCXDUM
      XEXFXGXHYLJYDGUSZYEKYDVMYLUUQVAUUEKYDJGXIUMJXQVMUUQYEVAPJXQYDGXJUMXKVEXLX
      M $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F f $.  $d G a b f g h $.  $d X a b f g h $.
    cayleyi.1 $e |- G e. Grp $.
    cayleyi.2 $e |- X = ran G $.
    cayleyi.3 $e |- H = ( SymGrp ` X ) $.
    cayleyi.4 $e |- F = { <. g , h >. | ( g e. X /\ h = { <. a , b >. |
                          ( a e. X /\ b = ( g G a ) ) } ) } $.
    cayleyi.5 $e |- Y = ran F $.
    cayleyi.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Cayley's Theorem (constructive version): given group ` G ` , ` F ` is an
       isomorphism between ` G ` and the subgroup ` S ` of the symmetry group
       ` H ` on the underlying set ` X ` of ` G ` .  (Contributed by Paul
       Chapman, 3-Mar-2008.) $)
    cayleyi $p |- ( S e. ( SubGrp ` H ) /\ F e. ( G GrpIso S ) ) $=
      ( vf cv wf1o cab cgi cfv eqid cayleylem3 ) GGQRSQTZAEUAUBZQBCDEFGHIJKUEUC
      LUFUCMNOPUD $.
      $( [18-Mar-2008] $) $( [3-Mar-2008] $)
  $}

  ${
    $d F f h $.  $d G a b g j $.  $d G f h $.  $d H f h $.  $d Q a b g j $.
    $d Q f h $.  $d S h $.  $d X a b g j $.
    cayleythlem.1 $e |- Q = { <. <. x , x >. , x >. } $.
    cayleythlem.2 $e |- X = ran if ( G e. Grp , G , Q ) $.
    cayleythlem.3 $e |- H = ( SymGrp ` X ) $.
    cayleythlem.4 $e |- F = { <. g , j >. | ( g e. X /\ j = { <. a , b >. |
      ( a e. X /\ b = ( g if ( G e. Grp , G , Q ) a ) ) } ) } $.
    cayleythlem.5 $e |- Y = ran F $.
    cayleythlem.6 $e |- S = ( H |` ( Y X. Y ) ) $.
    $( Lemma for ~ cayleyth . $)
    cayleythlem $p |- ( G e. Grp ->
        E. f E. h e. ( SubGrp ` ( SymGrp ` ran G ) ) f e. ( G GrpIso h ) ) $=
      ( cgr wcel cv cgiso co csubg cfv wrex wex crn csymgrp cif wceq opreq1
      eleq2d rexbidv exbidv wa cop csn visset grpsn eqeltri elimel cayleyi
      opreq2 rcla4ev ax-mp copab cvv rnexg fopabex2 eleq1 cla4ev dedth iftrue
      rneqd syl5eq fveq2d rexeqdv mpbid ) IUAUBZDUCZIFUCZUDUEZUBZFJUFUGZUHZDUIZ
      WFFIUJZUKUGZUFUGZUHZDUIWBWIWCWBIBULZWDUDUEZUBZFWGUHZDUIZIBIWNUMZWHWQDWSWF
      WPFWGWSWEWOWCIWNWDUDUNUOUPUQHWOUBZFWGUHZWRCWGUBHWNCUDUEZUBZURXACEGHWNJKLM
      NIBUABAUCZXDUSXDUSUTUAOXDAVAVBVCVDZPQRSTVEWTXCFCWGWDCUMWOXBHWDCWNUDVFUOVG
      VHWQXADHEGKMUCZKUBNUCEUCXFWNUEUMURMNVIHKWNUJZVJPWNUAUBXGVJUBXEWNUAVKVHVCR
      VLWCHUMWPWTFWGWCHWOVMUPVNVHVOWBWHWMDWBWFFWGWLWBJWKUFWBKUKUGWKJWBKWJUKWBXG
      WJKWBWNIWBIBVPVQPVRVSQVRVSVTUQWA $.
      $( [3-Mar-2008] $)
  $}

  ${
    $d G a b f g h j $.  $d a b f g h j x $.
    $( Cayley's Theorem (existence version): every group ` G ` is isomorphic to
       a subgroup of the symmetry group on the underlying set of ` G ` .  (For
       any group ` G ` there exists an isomorphism ` f ` between ` G ` and a
       subgroup ` h ` of the symmetry group on the underlying set of ` G ` .)
       (Contributed by Paul Chapman, 3-Mar-2008.) $)
    cayleyth $p |- ( G e. Grp -> E. f E. h e. ( SubGrp ` ( SymGrp ` ran G ) )
                                      f e. ( G GrpIso h ) ) $=
      ( vx vg vj va vb cv cop csn cgr wcel cif crn csymgrp cfv co wceq wa
      copab cxp cres eqid cayleythlem ) DDIZUFJUFJKZCLMCUGNZOZPQZEIZUIMFIGIZUIM
      HIUKULUHRSTGHUASTEFUAZOZUNUBUCZAEBFUMCUJUIUNGHUGUDUIUDUJUDUMUDUNUDUOUDUE
      $.
      $( [3-Mar-2008] $)
  $}

