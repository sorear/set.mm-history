$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Miscellaneous theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Division of both sides of 'less than or equal to' by a nonnegative
       number. $)
    lediv2aALT $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. RR /\ 0 < B ) /\
                       ( C e. RR /\ 0 <_ C ) ) ->
                     ( A <_ B -> ( C / B ) <_ ( C / A ) ) ) $=
      ( cr wcel cc0 clt wbr wa cle w3a c1 cdiv co cmul wi df-3an wne gt0ne0
      rereccl syldan anim12i ancoms 3adant3 simp3 sylanbrc lemul2aALT syl wb
      lerec wceq cc recn adantr jca 3anass sylibr divrec 3adant1 3adant2
      breq12d 3imtr4d ) ADEZFAGHZIZBDEZFBGHZIZCDEZFCJHZIZKZLBMNZLAMNZJHZCVMONZC
      VNONZJHZABJHZCBMNZCAMNZJHVLVMDEZVNDEZVKKZVOVRPWDWBWCIZVKVLWBWCVKQVEVHWEVK
      VHVEWEVHWBVEWCVFVGBFRZWBBSZBTUAVCVDAFRZWCASZATUAUBUCUDVEVHVKUEUFVMVNCUGUH
      VEVHVSVOUIVKABUJUDVLVTVPWAVQJVHVKVTVPUKZVEVKVHWJVKVHIZCULEZBULEZWFKZWJWKW
      LWMWFIZIWNVKWLVHWOVIWLVJCUMUNZVHWMWFVFWMVGBUMUNWGUOUBWLWMWFUPUQCBURUHUCUS
      VEVKWAVQUKZVHVKVEWQVKVEIZWLAULEZWHKZWQWRWLWSWHIZIWTVKWLVEXAWPVEWSWHVCWSVD
      AUMUNWIUOUBWLWSWHUPUQCAURUHUCUTVAVB $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2sqlei.1 $e |- A e. CC $.
    abs2sqlei.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqlei $p |- ( ( abs ` A ) <_ ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr c2 cexp co wb absge0i abscli le2sqi mp2an ) EAFGZH
      IEBFGZHIRSHIRJKLSJKLHIMACNBDNRSACOBDOPQ $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2sqlti.1 $e |- A e. CC $.
    abs2sqlti.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqlti $p |- ( ( abs ` A ) < ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr clt c2 cexp co wb absge0i abscli lt2sqi mp2an ) EA
      FGZHIEBFGZHISTJISKLMTKLMJINACOBDOSTACPBDPQR $.
      $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqle $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) <_ ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv cle wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqlei dedth2h ) ACDZBCDZ
      AEFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJ
      KZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSV
      DVGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUE
      UF $.
      $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares. $)
    abs2sqlt $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) < ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv clt wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqlti dedth2h ) ACDZBCDZ
      AEFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJ
      KZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSV
      DVGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUE
      UF $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2difi.1 $e |- A e. CC $.
    abs2difi.2 $e |- B e. CC $.
    $( Difference of absolute values. $)
    abs2difi $p |- ( ( abs ` A ) - ( abs ` B ) ) <_ ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2dif mp2an ) AEFBEFAGHBGHIJABIJGHKL
      CDABMN $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2difabsi.1 $e |- A e. CC $.
    abs2difabsi.2 $e |- B e. CC $.
    $( Absolute value of difference of absolute values. $)
    abs2difabsi $p |- ( abs ` ( ( abs ` A ) - ( abs ` B ) ) ) <_
                     ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2difabs mp2an ) AEFBEFAGHBGHIJGHABI
      JGHKLCDABMN $.
      $( [7-Sep-2007] $)
  $}

