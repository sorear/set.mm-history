$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                     Miscellaneous theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    pm5.21ndd.1 $e |- ( ph -> ( ch -> ps ) ) $.
    pm5.21ndd.2 $e |- ( ph -> ( th -> ps ) ) $.
    pm5.21ndd.3 $e |- ( ph -> ( ps -> ( ch <-> th ) ) ) $.
    $( Eliminate an antecedent implied by each side of a biconditional,
       deduction version.  (Contributed by Paul Chapman, 21-Nov-2012.) $)
    pm5.21ndd $p |- ( ph -> ( ch <-> th ) ) $=
      ( wa simpl imp jca wb pm5.21nd ) ACDABHACHABACIACBEJKADHABADIADBFJKABCDLG
      JM $.
      $( [21-Nov-2012] $)
  $}

  $( One less than an integer greater than or equal to 2 is a positive
     integer.  (Contributed by Paul Chapman, 17-Nov-2012.) $)
  uz2m1nn $p |- ( N e. ( ZZ>= ` 2 ) -> ( N - 1 ) e. NN ) $=
    ( c2 cuz cfv wcel cz c1 clt wbr wa cmin co cn eluz2b1 wb 1z znnsub mpan
    biimpa sylbi ) ABCDEAFEZGAHIZJAGKLMEZANUAUBUCGFEUAUBUCOPGAQRST $.
    $( [23-Nov-2012] $) $( [17-Nov-2012] $)

  $( Membership in a curtailed finite sequence of integers.  (Contributed by
     Paul Chapman, 17-Nov-2012.) $)
  elfzm12 $p |- ( N e. NN ->
                  ( M e. ( 1 ... ( N - 1 ) ) -> M e. ( 1 ... N ) ) ) $=
    ( cn wcel c1 cmin co cfz cuz cfv wss cz nnz cle wbr cr zre caddc lep1 wb
    1re lesubadd mp3an2 anidms mpbird syl peano2zm eluz mpancom fzss2 sseld )
    BCDZEBEFGZHGZEBHGZAULBUMIJDZUNUOKULBLDZUPBMUQUPUMBNOZUQBPDZURBQUSURBBERGNOZ
    BSUSURUTTZUSEPDUSVAUABEBUBUCUDUEUFUMLDUQUPURTBUGUMBUHUIUEUFUMEBUJUFUK $.
    $( [17-Nov-2012] $)

  ${
    $d F k $.  $d N k $.
    nn0seqcvg.1 $e |- F : NN0 --> NN0 $.
    nn0seqcvg.2 $e |- N = ( F ` 0 ) $.
    nn0seqcvg.3 $e |- ( k e. NN0 -> ( ( F ` ( k + 1 ) ) =/= 0 ->
                                      ( F ` ( k + 1 ) ) < ( F ` k ) ) ) $.
    $( A strictly-decreasing nonnegative integer sequence with initial term
       ` N ` reaches zero by the ` N ` th term.  Inference version.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    nn0seqcvg $p |- ( F ` N ) = 0 $=
      ( c1 wceq cfv cc0 eqid cn0 wf a1i cv wcel caddc co wne clt wbr wi adantl
      nn0seqcvgd ax-mp ) GGHZCBIJHGKUFABCLLBMUFDNCJBIHUFENAOZLPUGGQRBIZJSUHUGBI
      TUAUBUFFUCUDUE $.
      $( [31-Mar-2011] $)
  $}

  $( An integer modulo 1 is 0.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  zmod10 $p |- ( N e. ZZ -> ( N mod 1 ) = 0 ) $=
    ( cz wcel c1 cmo co cfl cfv cmin cc0 cr wceq zre modfrac syl flid opreq2d
    cc zcn subid 3eqtrd ) ABCZADEFZAAGHZIFZAAIFZJUBAKCUCUELAMANOUBUDAAIAPQUBARC
    UFJLASATOUA $.
    $( [26-Jun-2011] $)

  $( Identity law for modulo restricted to integers.  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zmodid2 $p |- ( ( M e. ZZ /\ N e. NN ) -> ( ( M mod N ) = M <->
                                              M e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel cn wa cmo co wceq cc0 cle wbr clt c1 cmin cfz cr crp wb zre nnrp
    modid2 syl2an nnz w3a 0z elfzm11 mpan 3anass syl6bb syl ibar bicomd
    sylan9bbr bitr4d ) ACDZBEDZFABGHAIZJAKLZABMLZFZAJBNOHPHDZUPAQDBRDURVASUQATB
    UAABUBUCUQVBUPVAFZUPVAUQBCDZVBVCSBUDVDVBUPUSUTUEZVCJCDVDVBVESUFAJBUGUHUPUSU
    TUIUJUKUPVAVCUPVAULUMUNUO $.
    $( [22-Jun-2011] $)

  $( Closure law for modulo restricted to integers.  (Contributed by Paul
     Chapman, 22-Jun-2011.) $)
  zmodfz $p |-
            ( ( M e. ZZ /\ N e. NN ) -> ( M mod N ) e. ( 0 ... ( N - 1 ) ) ) $=
    ( cz wcel cn wa cmo co cc0 c1 cmin cfz cle wbr clt w3a cn0 zmodcl nn0z syl
    cr crp zre nnrp modge0 syl2an modlt 3jca wb 0z nnz elfzm11 sylancr adantl
    mpbird ) ACDZBEDZFZABGHZIBJKHLHDZUSCDZIUSMNZUSBONZPZURVAVBVCURUSQDVAABRUSST
    UPAUADZBUBDZVBUQAUCZBUDZABUEUFUPVEVFVCUQVGVHABUGUFUHUQUTVDUIZUPUQICDBCDVIUJ
    BUKUSIBULUMUNUO $.
    $( [22-Jun-2011] $)

  ${
    $d F k $.  $d K k $.  $d M k $.  $d N k $.
    seqzresval2.1 $e |- S e. _V $.
    seqzresval2.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seqz ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seqzresval2 $p |- ( ( N e. ( ZZ>= ` M ) /\ K e. ( M ... N ) ) ->
                        ( ( <. M , S >. seqz ( F |` ( M ... N ) ) ) ` K ) =
                        ( ( <. M , S >. seqz F ) ` K ) ) $=
      ( vk cuz cfv wcel cfz co wa cv cres wceq wral cop cseqz elfzuzb biimpa
      simpld wss simprd fzss2 syl sseld fvres eqcomd syl6 r19.21aiv cvv resexg
      ax-mp seqzfveq syl11anc ) EDIJZKZCDELMZKZNZCURKZHOZBJZVDBUTPZJZQZHDCLMZRZ
      CDASZVFTMJZCVKBTMJZQVBVCECIJKZUSVAVCVNNURCDEUAUBZUCVBVHHVIVBVDVIKVDUTKZVH
      VBVIUTVDVBVNVIUTUDVBVCVNVOUECDEUFUGUHVPVGVEVDUTBUIUJUKULVCVJNVMVLAHBVFDCF
      GBUMKVFUMKGBUTUMUNUOUPUJUQ $.
      $( [22-Jun-2011] $)
  $}

  ${
    seq1resval.1 $e |- S e. _V $.
    seq1resval.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq1 ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seq1resval $p |- ( N e. NN ->
        ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` N ) = ( ( S seq1 F ) ` N ) ) $=
      ( cn wcel c1 cop cfz co cres cseqz cfv wceq cseq1 cuz elnnuz seqzresval
      sylbi cvv resexg ax-mp seq1seqz fveq1i eqeq12i sylibr ) CFGZCHAIZBHCJKZLZ
      MKZNZCUIBMKZNZOZCAUKPKZNZCABPKZNZOUHCHQNGUPCRABHCDESTURUMUTUOCUQULAUKDBUA
      GUKUAGEBUJUAUBUCUDUECUSUNABDEUDUEUFUG $.
      $( [22-Jun-2011] $)
  $}

  ${
    seq1resval2.1 $e |- S e. _V $.
    seq1resval2.2 $e |- F e. _V $.
    $( A restriction of its characteristic function that doesn't change the
       value of the ` seq1 ` function.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    seq1resval2 $p |- ( ( N e. NN /\ K e. ( 1 ... N ) ) ->
        ( ( S seq1 ( F |` ( 1 ... N ) ) ) ` K ) = ( ( S seq1 F ) ` K ) ) $=
      ( cn wcel c1 cfz co wa cop cres cseqz cfv wceq cseq1 cuz elnnuz
      seqzresval2 sylanb cvv resexg ax-mp seq1seqz fveq1i eqeq12i sylibr ) DGHZ
      CIDJKZHZLCIAMZBUKNZOKZPZCUMBOKZPZQZCAUNRKZPZCABRKZPZQUJDISPHULUSDTABCIDEF
      UAUBVAUPVCURCUTUOAUNEBUCHUNUCHFBUKUCUDUEUFUGCVBUQABEFUFUGUHUI $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d A f k m s $.  $d F f s $.  $d K f m s $.  $d N f k m s $.  $d S f s $.
    $( Closure of the partial values of the 1-based recursive sequence builder
       on a finite sequence.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
    fseq1cl $p |- ( ( ( A e. B /\ S : ( A X. A ) --> A ) /\
                  ( N e. ZZ /\ F : ( 1 ... N ) --> A /\ K e. ( 1 ... N ) ) ) ->
                    ( ( S seq1 F ) ` K ) e. A ) $=
      ( vf vs vm vk cz wcel c1 cfz co wf w3a cxp wa cseq1 cfv wi cvv oprex fex
      mpan2 xpexg anidms sylan2 anim12i 3impb 3comr cv wal 1z cle wbr elfz1
      caddc wceq fveq2 eleq1d imbi2d cn elnnz1 visset seq11 cuz elnnuz eluzfz1
      sylbi ffvelrn ancoms syl5eqel adantrr ex sylbir 3adant1 clt seq1p1
      adantr adantl 3ad2ant2 cn0 nnnn0 cc0 elnn0z wb peano2z elfz mp3an2 sylan
      cr 1re zre addge02 sylancr zltp1le anbi12d bitr4d exbiri com23 expdimp
      syl imp32 foprrn 3com23 syl3an2 eqeltrd 3expia anassrs expcom ancomsd
      sylanbr 3impa impcom adantll a2d fzind sylbid mpan imp 19.21aivv feq1
      bi2anan9 opreq12 fveq1d imbi12d cla42gv mpan9 com12 exp3a mpdi imp3a
      3com12 ) FKLZMFNOZADPZEYQLZQABLZAARZACPZSZECDTOZUAZALZYRYPYSUUCUUFUBZYRYP
      YSUUGUUCYRYPYSSZSUUFUUCYRUUHUUFYTUUBYRUUHUUFUBZYTYRUUBUUIUUHYTYRUUBQZUUFU
      UHUUJDUCLZCUCLZSZUUFYRUUBYTUUMYRUUBYTUUMYRUUKUUBYTSUULYRYQUCLUUKMFNUDYQAU
      CDUEUFYTUUBUUAUCLZUULYTUUNAABBUGUHUUAAUCCUEUIUJUKULUUJUUHUUMUUFUBUUJUUHUU
      MUUFYRUUBUUHUUMSZUUFUBYTUUOYRUUBSZUUFUUHYQAGUMZPZUUAAHUMZPZSZEUUSUUQTOZUA
      ZALZUBZHUNGUNUUMUUPUUFUBZUUHUVEGHYPYSUVEMKLZYPYSUVEUBUOUVGYPSZYSEKLMEUPUQ
      EFUPUQQZUVEEMFURUVHUVIUVEUVAIUMZUVBUAZALZUBUVAMUVBUAZALZUBZUVAJUMZUVBUAZA
      LZUBUVAUVPMUSOZUVBUAZALZUBUVEIJEMFUVJMUTZUVLUVNUVAUWBUVKUVMAUVJMUVBVAVBVC
      UVJUVPUTZUVLUVRUVAUWCUVKUVQAUVJUVPUVBVAVBVCUVJUVSUTZUVLUWAUVAUWDUVKUVTAUV
      JUVSUVBVAVBVCUVJEUTZUVLUVDUVAUWEUVKUVCAUVJEUVBVAVBVCYPMFUPUQZUVOUVGYPUWFS
      FVDLZUVOFVEUWGUVAUVNUWGUURUVNUUTUWGUURSUVMMUUQUAZAUUSUUQHVFZGVFZVGUURUWGU
      WHALZUWGUURMYQLZUWKUWGFMVHUALUWLFVIMFVJVKYQAMUUQVLUIVMVNVOVPVQVRUVHUVPKLZ
      MUVPUPUQZUVPFVSUQZQZSUVAUVRUWAYPUWPUVAUVRUWAUBZUBZUVGUWPYPUWRUWMUWNUWOYPU
      WRUBUWMUWNSZUWOSYPUWRUWSUWOYPUWRUWSUVPVDLZUWOYPSZUWRUVPVEUWTUXASZUUTUURUW
      QUUTUURSUXBUWQUUTUURUXBUWQUUTUURUXBSZUVRUWAUUTUXCUVRQUVTUVQUVSUUQUAZUUSOZ
      AUXCUUTUVTUXEUTZUVRUXBUXFUURUWTUXFUXAUVPUUSUUQUWIUWJVTWAWBWCUXCUUTUXDALZU
      VRUXEALZUXBUURUVSYQLZUXGUWTUWOYPUXIUWTUVPWDLZUWOYPUXIUBZUBZUVPWEUXJUWMWFU
      VPUPUQZSUXLUVPWGUWMUXMUWOUXKUWMYPUXMUWOSZUXIUWMYPUXIUXNUWMYPSZUXIMUVSUPUQ
      ZUVSFUPUQZSZUXNUWMUVSKLZYPUXIUXRWHZUVPWIUXSUVGYPUXTUOUVSMFWJWKWLUXOUXMUXP
      UWOUXQUWMUXMUXPWHZYPUWMMWMLUVPWMLUYAWNUVPWOMUVPWPWQWAUVPFWRWSWTXAXBXCVKXD
      XEYQAUVSUUQVLUIUUTUVRUXGUXHUVQUXDAAAUUSXFXGXHXIXJXKXLXMXNXKVPXOXPXQXRXSVP
      XTYAYBYCUVEUVFGHDCUCUCUUQDUTZUUSCUTZSZUVAUUPUVDUUFUYBUURYRUYCUUTUUBYQAUUQ
      DYDUUAAUUSCYDYEUYDUVCUUEAUYDEUVBUUDUYCUYBUVBUUDUTUUSCUUQDTYFVMYGVBYHYIYJY
      KVRYLYKYMYKXGXJYNYKUKYOXP $.
      $( [26-Jun-2011] $)
  $}

  $( Absorbtion law for modulo.  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  modaddabs $p |- ( ( A e. RR /\ B e. RR /\ C e. RR+ ) ->
            ( ( ( A mod C ) + ( B mod C ) ) mod C ) = ( ( A + B ) mod C ) ) $=
    ( cr wcel crp w3a cmo co caddc cc wceq wa modcl recn syl 3adant2 3adant1
    addcom syl11anc opreq1d simpl jca simpr modabs2 modadd1 syl111anc 3ad2ant2
    3simpc 3eqtr2rd eqtr4d ) ADEZBDEZCFEZGZACHIZBCHIZJIZCHIUQUPJIZCHIZABJICHIZU
    OURUSCHUOUPKEZUQKEZURUSLULUNVBUMULUNMZUPDEZVBACNZUPOPQZUMUNVCULUMUNMZUQDEZV
    CBCNZUQOPRUPUQSTUAUOUTBUPJIZCHIZUPBJIZCHIZVAUOVIUMMZVEUNMZUQCHIUQLZUTVLLUMU
    NVOULVHVIUMVJUMUNUBUCRULUNVPUMVDVEUNVFULUNUDUCQUMUNVQULBCUERUQBUPCUFUGUOVMV
    KCHUOVBBKEZVMVKLVGUMULVRUNBOUHUPBSTUAUOVEULMZVHUPCHIUPLZVNVALULUNVSUMVDVEUL
    VFULUNUBUCQULUMUNUIULUNVTUMACUEQUPABCUFUGUJUK $.
    $( [22-Jun-2011] $)

  $( An integer is a member of a 0-based finite set of sequential integers iff
     its successor is a member of the corresponding 1-based set.  (Contributed
     by Paul Chapman, 22-Jun-2011.) $)
  elfzp1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 0 ... ( N - 1 ) ) <-> ( K + 1 ) e. ( 1 ... N ) ) ) $=
    ( cz wcel wa c1 caddc co cfz cmin cc0 wb peano2z 1z fzsubel mpanl1 mpanr2
    sylan2 ancoms cc wceq zcn ax-1cn pncan sylancl subidi opreq1i a1i eleq12d
    adantr bitr2d ) ACDZBCDZEAFGHZFBIHDZUNFJHZFFJHZBFJHZIHZDZAKURIHZDZUMULUOUTL
    ZULUMUNCDZVCAMUMVDFCDZVCNVEUMVDVEEVCNUNFFBOPQRSULUTVBLUMULUPAUSVAULATDFTDUP
    AUAAUBUCAFUDUEUSVAUAULUQKURIFUCUFUGUHUIUJUK $.
    $( [22-Jun-2011] $)

  $( An integer is a member of a 1-based finite set of sequential integers iff
     its predecessor is a member of the corresponding 0-based set.
     (Contributed by Paul Chapman, 22-Jun-2011.) $)
  elfzm1b $p |- ( ( K e. ZZ /\ N e. ZZ ) ->
                ( K e. ( 1 ... N ) <-> ( K - 1 ) e. ( 0 ... ( N - 1 ) ) ) ) $=
    ( cz wcel c1 cfz co cmin cc0 wb wa 1z fzsubel mpanl1 mpanr2 ax-1cn subidi
    opreq1i eleq2i syl6bb ancoms ) BCDZACDZAEBFGDZAEHGZIBEHGZFGZDZJUBUCKUDUEEEH
    GZUFFGZDZUHUBUCECDZUDUKJZLULUBUCULKUMLAEEBMNOUJUGUEUIIUFFEPQRSTUA $.
    $( [22-Jun-2011] $)

  ${
    $( Division of both sides of 'less than or equal to' by a nonnegative
       number.  (Contributed by Paul Chapman, 7-Sep-2007.) $)
    lediv2aALT $p |- ( ( ( A e. RR /\ 0 < A ) /\ ( B e. RR /\ 0 < B ) /\
                       ( C e. RR /\ 0 <_ C ) ) ->
                     ( A <_ B -> ( C / B ) <_ ( C / A ) ) ) $=
      ( cr wcel cc0 clt wbr wa cle w3a c1 cdiv co cmul wi wne gt0ne0 rereccl
      syldan anim12i ancoms 3adant3 simp3 df-3an sylanbrc lemul2a ex syl wb
      lerec wceq cc recn adantr jca 3anass sylibr divrec 3adant1 3adant2
      breq12d 3imtr4d ) ADEZFAGHZIZBDEZFBGHZIZCDEZFCJHZIZKZLBMNZLAMNZJHZCVNONZC
      VOONZJHZABJHZCBMNZCAMNZJHVMVNDEZVODEZVLKZVPVSPVMWCWDIZVLWEVFVIWFVLVIVFWFV
      IWCVFWDVGVHBFQZWCBRZBSTVDVEAFQZWDARZASTUAUBUCVFVIVLUDWCWDVLUEUFWEVPVSVNVO
      CUGUHUIVFVIVTVPUJVLABUKUCVMWAVQWBVRJVIVLWAVQULZVFVLVIWKVLVIIZCUMEZBUMEZWG
      KZWKWLWMWNWGIZIWOVLWMVIWPVJWMVKCUNUOZVIWNWGVGWNVHBUNUOWHUPUAWMWNWGUQURCBU
      SUIUBUTVFVLWBVRULZVIVLVFWRVLVFIZWMAUMEZWIKZWRWSWMWTWIIZIXAVLWMVFXBWQVFWTW
      IVDWTVEAUNUOWJUPUAWMWTWIUQURCAUSUIUBVAVBVC $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2sqlei.1 $e |- A e. CC $.
    abs2sqlei.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares.
       (Contributed by Paul Chapman, 7-Sep-2007.) $)
    abs2sqlei $p |- ( ( abs ` A ) <_ ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr c2 cexp co wb absge0i abscli le2sqi mp2an ) EAFGZH
      IEBFGZHIRSHIRJKLSJKLHIMACNBDNRSACOBDOPQ $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2sqlti.1 $e |- A e. CC $.
    abs2sqlti.2 $e |- B e. CC $.
    $( The absolute values of two numbers compare as their squares.
       (Contributed by Paul Chapman, 7-Sep-2007.) $)
    abs2sqlti $p |- ( ( abs ` A ) < ( abs ` B ) <->
                     ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) $=
      ( cc0 cabs cfv cle wbr clt c2 cexp co wb absge0i abscli lt2sqi mp2an ) EA
      FGZHIEBFGZHISTJISKLMTKLMJINACOBDOSTACPBDPQR $.
      $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares.
       (Contributed by Paul Chapman, 7-Sep-2007.) $)
    abs2sqle $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) <_ ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) <_ ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv cle wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqlei dedth2h ) ACDZBCDZ
      AEFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJ
      KZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSV
      DVGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUE
      UF $.
      $( [7-Sep-2007] $)
  $}

  ${
    $( The absolute values of two numbers compare as their squares.
       (Contributed by Paul Chapman, 7-Sep-2007.) $)
    abs2sqlt $p |- ( ( A e. CC /\ B e. CC ) ->
                      ( ( abs ` A ) < ( abs ` B ) <->
                        ( ( abs ` A ) ^ 2 ) < ( ( abs ` B ) ^ 2 ) ) ) $=
      ( cc wcel cabs cfv clt wbr c2 cexp co wb cc0 cif wceq fveq2 breq1d
      opreq1d bibi12d breq2d opreq1 syl 0cn elimel abs2sqlti dedth2h ) ACDZBCDZ
      AEFZBEFZGHZUIIJKZUJIJKZGHZLUGAMNZEFZUJGHZUPIJKZUMGHZLUPUHBMNZEFZGHZURVAIJ
      KZGHZLABMMAUOOZUKUQUNUSVEUIUPUJGAUOEPZQVEULURUMGVEUIUPIJVFRQSBUTOZUQVBUSV
      DVGUJVAUPGBUTEPZTVGUJVAOZUSVDLVHVIUMVCURGUJVAIJUATUBSUOUTAMCUCUDBMCUCUDUE
      UF $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2difi.1 $e |- A e. CC $.
    abs2difi.2 $e |- B e. CC $.
    $( Difference of absolute values.  (Contributed by Paul Chapman,
       7-Sep-2007.) $)
    abs2difi $p |- ( ( abs ` A ) - ( abs ` B ) ) <_ ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2dif mp2an ) AEFBEFAGHBGHIJABIJGHKL
      CDABMN $.
      $( [7-Sep-2007] $)
  $}

  ${
    abs2difabsi.1 $e |- A e. CC $.
    abs2difabsi.2 $e |- B e. CC $.
    $( Absolute value of difference of absolute values.  (Contributed by Paul
       Chapman, 7-Sep-2007.) $)
    abs2difabsi $p |- ( abs ` ( ( abs ` A ) - ( abs ` B ) ) ) <_
                     ( abs ` ( A - B ) ) $=
      ( cc wcel cabs cfv cmin co cle wbr abs2difabs mp2an ) AEFBEFAGHBGHIJGHABI
      JGHKLCDABMN $.
      $( [7-Sep-2007] $)
  $}

$( (End of Paul Chapman's mathbox.) $)


