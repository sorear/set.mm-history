$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Added equality theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.
    raleq1i.1 $e |- A = B $.
    $( Equality inference for restricted universal qualifier. $)
    raleq1i $p |- ( A. x e. A ph <-> A. x e. B ph ) $=
      ( wceq wral wb raleq1 ax-mp ) CDFABCGABDGHEABCDIJ $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    $d A x $.  $d B x $.  $d ph x $.
    ralbieq1dv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    ralbieq1dv.2 $e |- ( ph -> A = B ) $.
    $( Equivalence/equality deducation for restricted universal qualifier. $)
    ralbieq1dv $p |- ( ph -> ( A. x e. A ps <-> A. x e. B ch ) ) $=
      ( wral ralbidv raleq1d bitrd ) ABDEICDEICDFIABCDEGJACDEFHKL $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq1d.1 $e |- ( ph -> F = G ) $.
    $( Equality deduction for function predicate with domain. $)
    fneq1d $p |- ( ph -> ( F Fn A <-> G Fn A ) ) $=
      ( wceq wfn wb fneq1 syl ) ACDFCBGDBGHEBCDIJ $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function predicate with domain. $)
    fneq2d $p |- ( ph -> ( F Fn A <-> F Fn B ) ) $=
      ( wceq wfn wb fneq2 syl ) ABCFDBGDCGHEBCDIJ $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq12d.1 $e |- ( ph -> F = G ) $.
    fneq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for function predicate with domain. $)
    fneq12d $p |- ( ph -> ( F Fn A <-> G Fn B ) ) $=
      ( wfn fneq1d fneq2d bitrd ) ADBHEBHECHABDEFIABCEGJK $.
      $( [26-Jun-2011] $) $( [26-Jun-2011] $)
  $}

  ${
    feq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for functions. $)
    feq2d $p |- ( ph -> ( F : A --> C <-> F : B --> C ) ) $=
      ( wceq wf wb feq2 syl ) ABCGBDEHCDEHIFBCDEJK $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    feq12d.1 $e |- ( ph -> F = G ) $.
    feq12d.2 $e |- ( ph -> A = B ) $.
    $( Equality deduction for functions. $)
    feq12d $p |- ( ph -> ( F : A --> C <-> G : B --> C ) ) $=
      ( wf feq1d feq2d bitrd ) ABDEIBDFICDFIABDEFGJABCDFHKL $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    feq23i.1 $e |- A = C $.
    feq23i.2 $e |- B = D $.
    $( Equality inference for functions. $)
    feq23i $p |- ( F : A --> B <-> F : C --> D ) $=
      ( wceq wf wb feq23 mp2an ) ACHBDHABEICDEIJFGABCDEKL $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    feq1i.1 $e |- F = G $.
    $( Equality inference for functions. $)
    feq1i $p |- ( F : A --> B <-> G : A --> B ) $=
      ( wceq wf wb feq1 ax-mp ) CDFABCGABDGHEABCDIJ $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    fneq1i.1 $e |- F = G $.
    $( Equality inference for function predicate with domain. $)
    fneq1i $p |- ( F Fn A <-> G Fn A ) $=
      ( wceq wfn wb fneq1 ax-mp ) BCEBAFCAFGDABCHI $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    reseq2i.1 $e |- A = B $.
    $( Equality inference for restrictions. $)
    reseq2i $p |- ( F |` A ) = ( F |` B ) $=
      ( wceq cres reseq2 ax-mp ) ABECAFCBFEDABCGH $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    reseq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for restrictions. $)
    reseq2d $p |- ( ph -> ( F |` A ) = ( F |` B ) ) $=
      ( wceq cres reseq2 syl ) ABCFDBGDCGFEBCDHI $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    ifbieq2i.1 $e |- ( ph <-> ps ) $.
    ifbieq2i.2 $e |- A = B $.
    $( Equivalence/equality inference for conditional operators. $)
    ifbieq2i $p |- if ( ph , C , A ) = if ( ps , C , B ) $=
      ( cif wb wceq ifbi ax-mp ifeq2 eqtri ) AECHZBECHZBEDHZABIOPJFABECKLCDJPQJ
      GBCDEMLN $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    ifbieq2d.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    ifbieq2d.2 $e |- ( ph -> A = B ) $.
    $( Equivalence/equality deduction for conditional operators. $)
    ifbieq2d $p |- ( ph -> if ( ps , C , A ) = if ( ch , C , B ) ) $=
      ( cif ifbid ifeq2d eqtrd ) ABFDICFDICFEIABCFDGJACDEFHKL $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    supeq1d.1 $e |- ( ph -> B = C ) $.
    $( Equality deduction for supremum. $)
    supeq1d $p |- ( ph -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( wceq csup supeq1 syl ) ACDGCBEHDBEHGFBCDEIJ $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    supeq1i.1 $e |- B = C $.
    $( Equality inference for supremum. $)
    supeq1i $p |- sup ( B , A , R ) = sup ( C , A , R ) $=
      ( wceq csup supeq1 ax-mp ) BCFBADGCADGFEABCDHI $.
      $( [22-Jun-2011] $) $( [22-Jun-2011] $)
  $}

