$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Elementary Number Theory - Lemmas
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a b x y z $.
    $( The image under negation of a bounded-above set of reals is bounded
       below.  (Contributed by Paul Chapman, 21-Mar-2011.) $)
    ublbneg $p |- ( E. x e. RR A. y e. A y <_ x ->
                    E. x e. RR A. y e. { z e. RR | -u z e. A } x <_ y ) $=
      ( vb va cv cle wbr wral cr wrex cneg wcel crab breq1 cbvralv rexbii wceq
      breq2 ralbidv cbvrexv bitri wi wa ssrab2 sseli negeq eleq1d elrab3
      biimpd mpcom rcla4v syl adantl wb lenegcon1 sylan2 sylibrd ex com23
      r19.21adv rcla4ev renegcl sylan syld r19.23aiv sylbir ) BGZAGZHIZBDJZAKLZ
      EGZFGZHIZEDJZFKLZVJVIHIZBCGZMZDNZCKOZJZAKLZVRVIVOHIZBDJZFKLVMVQWGFKVPWFEB
      DVNVIVOHPQRWGVLFAKVOVJSWFVKBDVOVJVIHTUAUBUCVQWEFKVOKNZVQVOMZVIHIZBWCJZWEW
      HVQWJBWCWHVIWCNZVQWJWHWLVQWJUDWHWLUEVQVIMZVOHIZWJWLVQWNUDZWHWLWMDNZWOVIKN
      ZWLWPWCKVIWBCKUFUGZWQWLWPWBWPCVIKVTVISWAWMDVTVIUHUIUJUKULVPWNEWMDVNWMVOHP
      UMUNUOWHWQWJWNUPWLVOVIUQWRURUSUTVAVBWHWKWEWIKNWKWEWHWDWKAWIKVJWISVSWJBWCV
      JWIVIHPUAVCVOVDVEUTVFVGVH $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A w z $.
    $( Two ways to express the image under negation of a set of integers.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    eqreznegel $p |- ( A C_ ZZ ->
                       { z e. RR | -u z e. A } = { z e. ZZ | -u z e. A } ) $=
      ( vw cz wss cv cneg wcel cr crab wa wi ssel cc caddc co zrevaddcl cc0
      negid 0z syl6eqel pm4.71i syl5bb recn syl5bi syl6 com23 imp3a pm3.27 a1i
      jcad zre anim1i impbid wceq negeq eleq1d elrab 3bitr4g eqrdv ) BDEZCAFZGZ
      BHZAIJZVDADJZVACFZIHZVGGZBHZKZVGDHZVJKZVGVEHVGVFHVAVKVMVAVKVLVJVAVHVJVLVA
      VJVHVLVAVJVIDHZVHVLLBDVIMVNVGNHZVLVHVNVOVGVIOPZDHZKVLVOVGVIQVOVQVOVPRDVGS
      TUAUBUCVGUDUEUFUGUHVKVJLVAVHVJUIUJUKVMVKLVAVLVHVJVGULUMUJUNVDVJAVGIVBVGUO
      VCVIBVBVGUPUQZURVDVJAVGDVRURUSUT $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d A w x y z $.
    $( The image under negation of a nonempty set of reals is nonempty.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    negn0 $p |- ( ( A C_ RR /\ A =/= (/) ) ->
                  { z e. RR | -u z e. A } =/= (/) ) $=
      ( vx vy cr wss c0 wne cv cneg wcel crab wex ssel wb renegcl wceq negeq
      eleq1d elrab3 syl cc recn negneg bitrd biimprd syli negex eleq1 cla4ev
      syl6 n0 syl6ibr 19.23adv syl5ib imp ) BEFZBGHZAIZJZBKZAELZGHZUQCIZBKZCMVC
      URUQVEVCCUQVEDIZVBKZDMZVCUQVEVDJZVBKZVHVEUQVDEKZVJBEVDNVKVJVEVKVJVIJZBKZV
      EVKVIEKVJVMOVDPVAVMAVIEUSVIQUTVLBUSVIRSTUAVKVLVDBVKVDUBKVLVDQVDUCVDUDUASU
      EUFUGVGVJDVIVDUHVFVIVBUIUJUKDVBULUMUNCBULUOUP $.
      $( [21-Mar-2011] $)

    $( The supremum of a bounded-above set of reals is the negation of the
       supremum of that set's image under negation.  (Contributed by Paul
       Chapman, 21-Mar-2011.) $)
    supminf $p |- ( ( A C_ RR /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
      sup ( A , RR , < ) = -u sup ( { z e. RR | -u z e. A } , RR , `' < ) ) $=
      ( vw cr wss c0 wne cv cle wbr wral wrex w3a cneg wcel crab clt ccnv csup
      wceq wa ssrab2 infmsup mp3an1 negn0 ublbneg syl2an 3impa negeq eleq1d
      elrab pm3.26bi adantl ssel2 wb elrab3 renegcl syl cc recn negneg bitrd
      eqrdav supeq1 3ad2ant1 negeqd eqtrd negcon2 infmrcl suprcl sylanc mpbid
      ) DFGZDHIZBJZAJZKLBDMAFNZOZCJZPZDQZCFRZFSTUAZDFSUAZPZUBZWFWEPUBZVTWEEJZPZ
      WDQZEFRZFSUAZPZWGVOVPVSWEWOUBZWDHIZVRVQKLBWDMAFNZWPVOVPUCZVSWDFGZWQWRWPWC
      CFUDZABEWDUEUFCDUGZABCDUHZUIUJVTWNWFVOVPWNWFUBZVSVOWMDUBXDVOAWMDFVRWMQZVR
      FQZVOXEXFVRPZWDQZWLXHEVRFWJVRUBWKXGWDWJVRUKULZUMUNUODFVRUPXFXEVRDQZUQVOXF
      XEXHXJWLXHEVRFXIURXFXHXGPZDQZXJXFXGFQXHXLUQVRUSWCXLCXGFWAXGUBWBXKDWAXGUKU
      LURUTXFXKVRDXFVRVAQXKVRUBVRVBVRVCUTULVDVDUOVEFWMDSVFUTVGVHVIWEFQZWFFQZWHW
      IUQZVTWEVAQWFVAQXOXMXNWEWFVJWEVBWFVBUIVOVPVSXMWQWRXMWSVSWTWQWRXMXAABWDVKU
      FXBXCUIUJABDVLVMVN $.
      $( [21-Mar-2011] $)

    $( If a set of reals is bounded below, it is bounded below by an integer.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    lbzbi $p |- ( A C_ RR -> ( E. x e. RR A. y e. A x <_ y <->
                               E. x e. ZZ A. y e. A x <_ y ) ) $=
      ( vz cr wss cv cle wbr wral wrex cz ax-17 hbre1 wcel wi wa clt w3a
      ltletr ltle 3adant2 syld zre syl3an1 exp3a 3expia ssel2 syl5 expdimp
      com23 imp r19.21aiv ralim syl ex anasss expcom imdistand wceq breq1
      ralbidv rcla4ev syl6 ancomsd r19.23adv imp3a btwnz pm3.26d pm4.71i
      syl5ib r19.23ad zssre ssrexv ax-mp a1i impbid ) CEFZAGZBGZHIZBCJZAEKZWBAL
      KZVRWBWDAEVRAMWBALNVRWBVSEOZWDVRWBWEWDPVRWBQZWEDGZVSRIZDLKZQWDWEWFWEWIWDW
      EWFWIWDPZWEVRWBWJWEVRQZWBQWHWDDLWKWBWGLOZWHWDPZWKWLWBWMWKWHWLWBQZWDWKWHWN
      WDPWKWHQZWNWLWGVTHIZBCJZQWDWOWLWBWQWKWHWLWBWQPZPWKWLWHWRWLWKWHWRPZWLWEVRW
      SWLWEQZVRQZWHWRXAWHQZWAWPPZBCJWRXBXCBCXAWHVTCOZXCPXAXDWHXCWTVRXDWHXCPZWTV
      TEOZXEVRXDQWLWEXFXEWLWEXFSWHWAWPWGEOZWEXFWHWAQZWPPWLXGWEXFSXHWGVTRIZWPWGV
      SVTTXGXFXIWPPWEWGVTUAUBUCWGUDUEUFUGCEVTUHUIUJUKULUMWAWPBCUNUOUPUQURUKULUS
      WBWQAWGLVSWGUTWAWPBCVSWGVTHVAVBVCVDUPUKVEUJVFUQURVGWEWIWEWIVSWGRIDLKDDVSV
      HVIVJVKUPUKVLWDWCPZVRLEFXJVMWBALEVNVOVPVQ $.
      $( [21-Mar-2011] $)

    $( The supremum of a bounded-above set of integers is a member of the set.
       (Contributed by Paul Chapman, 21-Mar-2011.) $)
    suprzcl $p |- ( ( A C_ ZZ /\ A =/= (/) /\ E. x e. RR A. y e. A y <_ x ) ->
                    sup ( A , RR , < ) e. A ) $=
      ( vz vw cz wss c0 wne cv cle wbr wral cr wrex w3a clt csup cneg wcel
      crab ccnv wceq supminf zssre sstr mpan2 syl3an1 wa cuz cfv infmssuzcl wi
      wal 3simp2 a1d ssrab2 eqreznegel sseq1d mpbiri sseld 3ad2ant1 breq2
      rcla4v com12 3ad2ant3 3jcad eluz biimpar 3impa syl6 19.21aiv dfss2
      sylibr 3expb adantlr negn0 sylan adantr sylanc ex exp3a r19.23adv 3impia
      ublbneg wb lbzbi ax-mp sylib syl3an3 negeq eleq1d cbvrabv syl6eleq elrab
      pm3.27bi syl eqeltrd ) CFGZCHIZBJZAJZKLBCMANOZPZCNQRZDJZSZCTZDNUAZNQUBRZS
      ZCCNGZWTXCXEXKUCWSABDCUDWSFNGXLUECFNUFUGZUHXDXJXBSZCTZANUAZTZXKCTZXDXJXIX
      PWSWTXBXAKLZBXIMZAFOZXJXITZXCWSWTYAYBWSWTUIZXTYBAFYCXBFTZXTYBYCYDXTUIZYBX
      IXBUJUKZGZXIHIZYBYCYEUIXIXBULWSYEYGWTWSYDXTYGWSYDXTPZEJZXITZYJYFTZUMZEUNY
      GYIYMEYIYKYDYJFTZXBYJKLZPYLYIYKYDYNYOYIYDYKWSYDXTUOUPWSYDYKYNUMXTWSXIFYJW
      SXIFGXHDFUAZFGXHDFUQWSXIYPFDCURUSUTVAVBXTWSYKYOUMYDYKXTYOXSYOBYJXIXAYJXBK
      VCVDVEVFVGYDYNYOYLYDYNUIYLYOXBYJVHVIVJVKVLEXIYFVMVNVOVPYCYHYEXLWTYHWSDCVQ
      XMVRVSVTWAWBWCWDXCXTANOZYAABDCWEXINGYQYAWFXHDNUQABXIWGWHWIWJXHXODANXFXBUC
      XGXNCXFXBWKWLWMWNXQXJNTXRXOXRAXJNXBXJUCXNXKCXBXJWKWLWOWPWQWR $.
      $( [21-Mar-2011] $)
  $}

  ${
    $d F k m $.  $d N k m $.  $d k m ph $.
    nn0seqcvgd.1 $e |- ( ph -> F : NN0 --> NN0 ) $.
    nn0seqcvgd.2 $e |- ( ph -> N = ( F ` 0 ) ) $.
    nn0seqcvgd.3 $e |- ( ( ph /\ k e. NN0 ) ->
      ( ( F ` ( k + 1 ) ) =/= 0 -> ( F ` ( k + 1 ) ) < ( F ` k ) ) ) $.
    $( A strictly-decreasing nonnegative integer sequence with initial term
       ` N ` reaches zero by the ` N ` th term.  Deduction version.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    nn0seqcvgd $p |- ( ph -> ( F ` N ) = 0 ) $=
      ( vm cfv cc0 cle wbr wceq cmin co cn0 wcel wi cv c1 caddc fveq2 opreq2
      breq12d imbi2d cr wf 0nn0 ffvelrn mpan2 syl eqeltrd nn0re leid eqbrtrrd
      cc recnd subid1 breq2d mpbird a1i clt wa wb posdif syl2an ancoms adantr
      breq1 adantl cz zltlem1 peano2nn0 nn0z zsubcl sylan sylan2 sylanc ax1cn
      subsub4 mp3an3 nn0cn bitrd 3bitr2d biimpa an1rs a1d wne wn ltletr zre
      syl3anc sylibd syland expdimp df-ne sylan2br pm2.61dan anasss expcom a2d
      3adant1 fnn0ind pm2.43i subid breqtrd nn0ge0 0re letri3 3syl biimprd
      mp2and ) ADCIZJKLZJXMKLZXMJMZAXMDDNOZJKAXMXQKLZDPQZXSDDKLZAXRRZAAHSZCIZDY
      BNOZKLZRAJCIZDJNOZKLZRZABSZCIZDYJNOZKLZRZAYJTUAOZCIZDYONOZKLZRZYAHBDDYBJM
      ZYEYHAYTYCYFYDYGKYBJCUBYBJDNUCUDUEYBYJMZYEYMAUUAYCYKYDYLKYBYJCUBYBYJDNUCU
      DUEYBYOMZYEYRAUUBYCYPYDYQKYBYOCUBYBYODNUCUDUEYBDMZYEXRAUUCYCXMYDXQKYBDCUB
      YBDDNUCUDUEYIXSAYHYFDKLADYFDKFADUFQZXTAXSUUDADYFPFAPPCUGZYFPQZEUUEJPQUUFU
      HPPJCUIUJUKULZDUMUKZDUNUKZUOAYGDYFKADUPQZYGDMADUUHUQZDURUKUSUTVAYJPQZYJDV
      BLZYNYSRXSUULUUMVCZAYMYRAUUNYMYRRZAUULUUMUUOAUULVCZUUMVCZYPJMZUUOUUQUURVC
      YRYMUUPUURUUMYRUUPUURVCZUUMYRUUSUUMJYLVBLZYPYLVBLZYRUUPUUMUUTVDZUURUULAUV
      BYJUFQUUDUVBUULAYJDVEYJUMUUHVFVGVHUURUVAUUTVDUUPYPJYLVBVIVJUUPUVAYRVDUURU
      UPUVAYPYLTNOZKLZYRYPVKQZYLVKQZUVAUVDVDUUPYPYLVLUUPYPPQZUVEUUEYOPQUVGAUULP
      PYOCUIEYJVMVFZYPVNUKAYJVKQZUVFUULDVKQZUVIUVFADYJVOAXSUVJUUGDVNUKVPYJVNVQZ
      VRUUPUVCYQYPKUUJYJUPQZUVCYQMZAUULUUJUVLTUPQUVMVSDYJTVTWAUUKYJWBVFUSWCZVHW
      DWEWFWGUUQYPJWHZUUOUURWIUUQUVOYMYRUUPUVOYMVCYRRUUMUUPYPYKVBLZYMYRUVOUUPUV
      PYMVCZUVAYRYPUFQZYKUFQZYLUFQZUVQUVARUUPYPYKYLWJUUPUVGUVRUVHYPUMUKUUPYKPQZ
      UVSUUEUULUWAAPPYJCUIEVPYKUMUKUUPUVFUVTUVKYLWKUKWLUVNWMGWNVHWOYPJWPWQWRWSW
      TXAXBXCUUGUUGUUIWLXDAUUJXQJMUUKDXEUKXFAXMPQZXOUUEXSUWBAPPDCUIEUUGVRZXMXGU
      KAXPXNXOVCZAUWBXMUFQZXPUWDVDZUWCXMUMUWEJUFQUWFXHXMJXIUJXJXKXL $.
      $( [31-Mar-2011] $)
  $}

  ${
    $d F k $.  $d N k $.
    nn0seqcvg.1 $e |- F : NN0 --> NN0 $.
    nn0seqcvg.2 $e |- N = ( F ` 0 ) $.
    nn0seqcvg.3 $e |- ( k e. NN0 -> ( ( F ` ( k + 1 ) ) =/= 0 ->
                                      ( F ` ( k + 1 ) ) < ( F ` k ) ) ) $.
    $( A strictly-decreasing nonnegative integer sequence with initial term
       ` N ` reaches zero by the ` N ` th term.  Inference version.
       (Contributed by Paul Chapman, 31-Mar-2011.) $)
    nn0seqcvg $p |- ( F ` N ) = 0 $=
      ( c1 wceq cfv cc0 eqid cn0 wf a1i cv wcel caddc co wne clt wbr wi adantl
      nn0seqcvgd ax-mp ) GGHZCBIJHGKUFABCLLBMUFDNCJBIHUFENAOZLPUGGQRBIZJSUHUGBI
      TUAUBUFFUCUDUE $.
      $( [31-Mar-2011] $)
  $}

  $( Membership in a nonegative set of upper integers implies membership in
     ` NN0 ` .  (Contributed by Paul Chapman, 22-Jun-2011.) $)
  eluznn0 $p |- ( ( N e. NN0 /\ M e. ( ZZ>= ` N ) ) -> M e. NN0 ) $=
    ( cuz cfv wcel cn0 cc0 wa uztrn nn0uz syl6eleqr eleq2i sylan2b ancoms ) ABC
    DEZBFEZAFEZOBGCDZEZQPOSHARFBAGIJKFRBJLMN $.
    $( [22-Jun-2011] $)

