$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some properties of specific numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two plus two equals 4. $)
  2p2e4 $p |- ( 2 _10 + 2 _10 ) = 4 _10  $=
    ( cl2 caddc co cn1 cl4 df-2 opreq2i 2re recn 1cn 4re cl3 df-4 df-3 opreq1i
    eqtr addass2r eqtr4 ) AABCADDBCZBCEASABFGADDEAHIJJEKIELDBCADBCZDBCMLTDBNOPQ
    R $.
    $([27-May-99]$)

  ${
    2times.1 $e |- A e. CC $.
    $( Two times a number. $)
    2times $p |- ( 2 _10 x. A ) = ( A + A ) $=
      ( cl2 cmulc co cn1 caddc df-2 opreq1i 1p1times eqtr ) CADEFFGEZADEAAGECLA
      DHIABJK $.
      $([1-Aug-99]$)
  $}

  $( Two times two equals 4. $)
  2t2e4 $p |- ( 2 _10 x. 2 _10 ) = 4 _10  $=
    ( cl2 cmulc co caddc cl4 2re recn 2times 2p2e4 eqtr ) AABCAADCEAAFGHIJ $.
    $([1-Aug-99]$)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 _10 ^ 2 _10 ) = 1 _10 $=
    ( cn1 cl2 cexp co cmulc 1cn sqval mulid1 eqtr ) ABCDAAEDAAFGAFHI $.
    $([22-Aug-99]$)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 _10 ^ 2 _10 ) = 4 _10 $=
    ( cl2 cexp co cmulc cl4 2re recn sqval 2t2e4 eqtr ) AABCAADCEAAFGHIJ $.
    $([22-Aug-99]$)

  $( The square root of 1 is 1. $)
  sqr1 $p |- ( sqr ` 1 _10 ) = 1 _10 $=
    ( cn1 cmulc co csqr cfv 1cn mulid1 fveq2i cn0 cle wbr wceq ax0re ax1re
    lt01 ltlei sqrsqid ax-mp eqtr3 ) AABCZDEZADEATADAFGHIAJKUAALIAMNOPANQRS $.
    $([31-Jul-99]$)

  $( The square root of 4 is 2. $)
  sqr4 $p |- ( sqr ` 4 _10 ) = 2 _10 $=
    ( cl4 csqr cfv cl2 wceq cmulc co cn0 clt wbr wb cle 4re 2re sqrsq ax0re
    ltle syl2an 4pos 2pos mp2an 2t2e4 cleqcomi mpbir ) ABCDEZADDFGZEZHAIJZHDIJZ
    UEUGKZHALJHDLJUJUHUIADMNOHAPMQHDPNQRSTUAUFAUBUCUD $.
    $([3-Aug-99]$)

  ${
    binom.1 $e |- A e. CC $.
    binom.2 $e |- B e. CC $.
    $( Binomial expansion. $)
    binom $p |- ( ( A + B ) ^ 2 _10 ) = ( ( ( A ^ 2 _10 ) + ( 2 _10 x.
                ( A x. B ) ) ) + ( B ^ 2 _10 ) ) $=
      ( caddc co cmulc cl2 cexp addcl distr1 distr2 mulcom opreq2i eqtr
      opreq12i mulcl addass opreq1i eqtr3 3eqtr sqval 2times 3eqtr4 ) ABEFZUEGF
      ZAAGFZABGFZUHEFZEFZBBGFZEFZUEHIFAHIFZHUHGFZEFZBHIFZEFUFUEAGFZUEBGFZEFUGUH
      EFZUHUKEFZEFZULUEABABCDJZCDKUQUSURUTEUQUGBAGFZEFUSABACDCLVCUHUGEBADCMNOAB
      BCDDLPUSUHEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZJVFBBDDQRVDUJUKEUGUHUHVEVFVFRS
      TUAUEVBUBUOUJUPUKEUMUGUNUIEACUBUHVFUCPBDUBPUD $.
      $([11-Aug-99]$)
  $}



  ${
    discrlem.1 $e |- A e. RR $.
    discrlem.2 $e |- B e. RR $.
    discrlem.3 $e |- C e. RR $.
    ${
      discrlem1.4 $e |- D = -u ( B / ( 2 _10 x. A ) ) $.
      ${
        discrlem1.5 $e |- 0 _10 < A $.
        $( Lemma for discriminant theorem. $)
        discrlem1 $p |- ( 0 _10 <_
                     ( ( ( A x. ( D ^ 2 _10 ) ) + ( B x. D ) ) + C ) <->
                     ( ( B ^ 2 _10 ) - ( 4 _10 x. ( A x. C ) ) ) <_ 0 _10 ) $=
          ( cn0 cl2 cexp co cmulc caddc cle wbr cneg cl4 cmin 4re recn mulcl
          mulzer1 cr wcel cdiv eleq1i 2re mulrcl 2pos ltne0 muln0 divrcl
          negrcl mpbir sqcl cc divcl negcl addcl distr1 breq12i clt wb 4pos
          mulgt0i ax0re sqrecl addrcl lemul2 ax-mp neg0 negdist2 cn1 divcan3
          opreq1i sqval mul2neg eqtr eqtr2 opreq12i mulass divmul13 divid
          opreq2i 1cn mulcom 3eqtr eqtr3 divcan2 negneg mulneg1 df-2 negeqi
          negdist negid addass addid2 3eqtr3 distr2 mulm1 2t2e4 divmuldiv
          eqtr4 mulneg2 3bitr4 subrcl leneg bitr4 ) JADKLMZNMZBDNMZOMZCOMZPQZJR
          ZBKLMZSACNMZNMZTMZRZPQZYAJPQSANMZJNMZYDXONMZPQZJYDXNNMZYDCNMZOMZPQXPY
          CYEJYFYJPYDSASUAUBZAEUBZUCZUDYDXNCYMXLXMAXKYLDDDUEUFBKANMZUGMZRZUEUFD
          YPUEHUHYOBYNFKAUIEUJZKAKUIUBZYLKUIUKULZAEIULZUMZUNUOZUPZUBUQUCBDBFUBZ
          DURUFYPURUFDYPURHUHYOBYNUVDYNYQUBZUVAUSZUTUPUCVACGUBZVBVCJYDVDQXPYGVE
          SAUAEVFIVGJXOYDVHXNCXLXMAXKEDUVCVIUJBDFUVCUJVJGVJSAUAEUJZVKVLXQJYBYJP
          VMYBXRRZXTOMYJXRXTXRBFVIZUBZSXSYKACYLUVGUCUCVNYHUVIYIXTOYHYDXRYDUGMZR
          ZNMYDUVLNMZRUVIXNUVMYDNXNVOBKUGMZNMZYONMZKRZUVONMZYONMZOMZUVMXLUVQXMU
          VTOYNKUGMZYOYONMZNMZXLUVQUWBAUWCXKNKAYRYLYSVPXKYPKLMZUWCDYPKLHVQUWEYP
          YPNMUWCYPYPUVBUBVRYOYOUVFUVFVSVTWAWBUWBYONMZYONMUWDUVQUWBYOYOYNKUVEYR
          YSUSUVFUVFWCUWFUVPYONUWFUVOYNYNUGMZNMUVOVONMUVPYNKBYNUVEYRUVDUVEYSUVA
          WDUWGVOUVONYNUVEUVAWEWFUVOVOBKUVDYRYSUSZWGWHWIVQWJWJXMUVSRZYPNMUVTBUW
          IDYPNKUVONMZBUWIKBYRUVDYSWKUVRRZUVONMUWJUWIUWKKUVONKYRWLVQUVRUVOKYRUT
          ZUWHWMWJWJHWBUVSYOUVRUVOUWLUWHUCZUVFVSVTWBUVPUVSOMZYONMUVORZYONMZUWAU
          VMUWNUWOYONVOUVROMZUVONMVORZUVONMUWNUWOUWQUWRUVONUWQVOUWRUWROMZOMZUWR
          UVRUWSVOOUVRVOVOOMZRUWSKUXAWNWOVOVOWGWGWPVTWFVOUWROMZUWROMJUWROMUWTUW
          RUXBJUWROVOWGWQVQVOUWRUWRWGVOWGUTZUXCWRUWRUXCWSWTVTVQVOUVRUVOWGUWLUWH
          XAUVOUWHXBWTVQUVPUVSYOVOUVOWGUWHUCUWMUVFXAUWPUVOYONMZRUVMUVOYOUWHUVFW
          MUVLUXDUVLBBNMZKYNNMZUGMUXDXRUXEYDUXFUGBUVDVRKKNMZANMYDUXFUXGSANXCVQK
          KAYRYRYLWCWJWBBKBYNUVDYRUVDUVEYSUVAXDXEWOXEWTVTWFYDUVLYDUVHUBZUVLXRYD
          UVJUVHSAYKYLSUAVFULYTUMZUNUBXFUVNXRYDXRUXHUVKUXIWKWOWISACYKYLUVGWCWBX
          EVCXGYAJXRXTUVJSXSUAACEGUJUJXHVHXIXJ $.
          $([6-Aug-99]$)
      $}

      ${
        discrlem2.5 $e |- ( D e. RR -> 0 _10 <_
                     ( ( ( A x. ( D ^ 2 _10 ) ) + ( B x. D ) ) + C ) ) $.
        $( Lemma for discriminant theorem. $)
      $( If a quadratic polynomial (with a positive high-order coefficient)
         is non-negative for all values of real number ` D ` , then its
         discriminant is non-positive.  We prove this by picking a specific
         value for ` D ` . $)
        discrlem2 $p |- ( 0 _10 < A ->
                      ( ( B ^ 2 _10 ) - ( 4 _10 x. ( A x. C ) ) ) <_ 0 _10 ) $=
          ( cn0 clt wbr cl2 cexp co cmulc caddc cle cl4 cmin wb cn1 cded cdiv
          cneg wceq id opreq2 opreq2d negeqd syl5eq opreq1d opreq12d breq2d
          opreq1 breq1d bi2bid cr ax1re keepel cleqid elimgt0 discrlem1 dedth
          wcel wne 2re mulgt0 2pos mpan ax0re mulrcl ltne syl necom sylibr
          divrclt negrclt 3syl eleq1i mpbid ) JAKLZJADMNOZPOZBDPOZQOZCQOZRLZBMN
          OZSACPOZPOZTOZJRLZWBWHWMUAJWBAUBUCZBMWNPOZUDOZUEZMNOZPOZBWQPOZQOZCQOZ
          RLZWISWNCPOZPOZTOZJRLZUAAUBAWNUFZWHXCWMXGXHWGXBJRXHWFXACQXHWDWSWEWTQX
          HAWNWCWRPXHUGXHDWQMNXHBMAPOZUDOZUEZWQDXHXJWPXHXIWOBUDAWNMPUHUIUJHUKZU
          LUMXHDWQBPXLUIUMULUNXHWLXFJRXHWKXEWITXHWJXDSPAWNCPUOUIUIUPUQWNBCWQWBA
          UBUREUSUTFGWQVAAVBVCVDWBDURVEZWHWBXKURVEZXMWBXIJVFZXJURVEXNWBJXIVFZXO
          WBJXIKLZXPJMKLWBXQMAVGEVHVIVJJXIVKMAVGEVLZVMVNXIJVOVPBXIFXRVQXJVRVSDX
          KURHVTVPIVNWA $.
          $([9-Aug-99]$)
      $}

    $}

    ${
      discrlem3.4 $e |- D = ( ( C + 1 _10 ) / -u B ) $.
      discrlem3.5 $e |- ( D e. RR -> 0 _10 <_
                     ( ( ( A x. ( D ^ 2 _10 ) ) + ( B x. D ) ) + C ) ) $.
      discrlem3 $p |- ( 0 _10 = A ->
                    ( ( B ^ 2 _10 ) - ( 4 _10 x. ( A x. C ) ) ) <_ 0 _10 ) $=
        ( cn0 wceq cl2 cexp co cl4 cmulc cmin cle wbr wn cn1 caddc clt cneg
        wne wi df-ne recn negne0 bitr3 wa wb cdiv cr wcel cc ax1re addrcl
        negrcl divrclt eleq1i sylibr jctil recnt im2an mulnegt 3syl cleqcomd
        df-neg opreq2i 3eqtr3g negcl divcan2t eqtrd breq1d adantr axmulrcl
        lesubadd2t mp3an3 ax0re mpan opreq1 syl sqclt mulzer2t sylan9eqr
        opreq1d addid2t breq2d mpbid mpbird lelt sylib exp sylbi ltpl1 mt2i
        a3i mulzer2 syl6eq sqval syl5eq syl5reqr opreq2d 4re mulzer1 opreq12d
        0cn subid sqrecl mulrcl subrcl eqle ) JAKZBLMNZOACPNZPNZQNZJKXRJRSXNXRJ
        JQNJXNXOJXQJQXNBBPNZJXOXNXSJBPNJXNBJBPBJKZXNXTTZXNCCUAUBNZUCSZYABUDZJUE
        ZXNYCTZUFYABJUEYEBJUGBBFUHZUIUJYEXNYFYEXNUKZYBCRSZYFYHJBDPNZQNZCRSZYIYE
        YLYIULXNYEYKYBCRYEYKYDYBYDUMNZPNZYBYEYJUDZYDDPNZYKYNYEYPYOYEBUNUOZDUNUO
        ZUKZBUPUOZDUPUOZUKYPYOKYEYRYQYEYMUNUOYRYBYDCUAGUQURZBFUSUTDYMUNHVAVBZFV
        CZYQYTYRUVABVDDVDZVEBDVFVGVHYJVIDYMYDPHVJVKYDYBBYGVLYBUVBUHVMVNVOVPYHYL
        JYJCUBNZRSZYEYLUVGULZXNYEYSYJUNUOZUVHUVDBDVQZJUNUOZUVIUVHUVKUVICUNUOUVH
        JYJCVRGVSVTWAVGVPYHJADLMNZPNZYJUBNZCUBNZRSZUVGYHUVOUVFJRYHUVNYJCUBYHUVN
        JYJUBNZYJYHUVMJYJUBXNYEUVMJUVLPNZJXNUVRUVMJAUVLPWBVHYEUVAUVLUPUOUVRJKYE
        YRUVAUVCUVEWCDWDUVLWEVGWFWGYEUVQYJKZXNYEUVIYJUPUOUVSYEYSUVIUVDUVJWCYJVD
        YJWHVGVPVNWGWIYEUVPXNYEYRUVPUVCIWCVPWJWKWJYBCUVBGWLWMWNWOCGWPWQWRWGBYGW
        SWTBYGXAXBXNXQOJPNJXNXPJOPXNJCPNXPJJACPWBCCGUHWSXCXDOOXEUHXFWTXGJXHXIWT
        XRJXOXQBFXJOXPXEACEGXKXKXLVTXMWC $.
        $([9-Aug-99]$)

    $}

    ${
      $d x A $.  $d x B $.  $d x C $.
      discrlem.4 $e |- A. x e. RR 0 _10 <_
                     ( ( ( A x. ( x ^ 2 _10 ) ) + ( B x. x ) ) + C ) $.
      $( If a quadratic polynomial (with a non-negative high-order coefficient)
         is non-negative for all values, then its discriminant is
         non-positive. $)
      discrlem $p |- ( 0 _10 <_ A ->
                      ( ( B ^ 2 _10 ) - ( 4 _10 x. ( A x. C ) ) ) <_ 0 _10 ) $=
        ( cn0 cle wbr clt wceq wo cl2 cexp co cl4 cmulc cmin ax0re leloe cdiv
        cneg cleqid cv caddc cr wral wcel wi opreq1 opreq2d opreq2 opreq12d
        opreq1d breq2d rcla4v ax-mp discrlem2 cn1 discrlem3 jaoi sylbi ) IBJKIB
        LKZIBMZNCOPQRBDSQSQTQIJKZIBUAEUBVEVGVFBCDCOBSQUCQUDZEFGVHUEIBAUFZOPQZSQ
        ZCVISQZUGQZDUGQZJKZAUHUIZVHUHUJIBVHOPQZSQZCVHSQZUGQZDUGQZJKZUKHVOWBAVHU
        HVIVHMZVNWAIJWCVMVTDUGWCVKVRVLVSUGWCVJVQBSVIVHOPULUMVIVHCSUNUOUPUQURUSU
        TBCDDVAUGQCUDUCQZEFGWDUEVPWDUHUJIBWDOPQZSQZCWDSQZUGQZDUGQZJKZUKHVOWJAWD
        UHVIWDMZVNWIIJWKVMWHDUGWKVKWFVLWGUGWKVJWEBSVIWDOPULUMVIWDCSUNUOUPUQURUS
        VBVCVD $.
        $([10-Aug-99]$)
    $}
  $}


$( To be done:  Define operations for constructing strings of digits $)

