$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Completeness Axiom and Supremums
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z A $.
    $( A non-empty, bounded-above set of reals has a supremum.  Stronger
       version of completeness axiom. $)
    sup2 $p |- ( ( A (_ RR /\ A =/= (/) /\
                  E. x e. RR A. y e. A ( y < x \/ y = x ) ) ->
               E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) ) $=
      ( cr wss c0 wne cv clt wbr weq wo wral wrex w3a wn wi wa wcel wex cn1
      caddc co ax1re axaddrcl mpan2 adantr a1i wal ssel axlttrn 3expb ancli
      sylan5 cn0 ax0re lt01 axltadd mpi mp3an1 mpan cc wceq recnt ax0id syl
      breq1d mpbid sylan5d exp4b com34 pm2.43d imp breq1 biimprcd adantl jaod
      exp syl6 com23 a2d 19.20dv df-ral 3imtr4g imp3a jcad oprex eleq1 breq2
      biraldv bi2and cla4ev 19.23adv cbvexv syl6ib df-rex imdistani df-3an
      3imtr4 axsup ) DEFZDGHZBIZAIZJKZBALZMZBDNZAEOZPZXBXCXFBDNZAEOZPZXEXDJKQBD
      NXFXDCIZJKZCDORBENSAEOXBXCSZXJSXQXMSXKXNXQXJXMXBXJXMRXCXBXEETZXISZAUAZXRX
      LSZAUAZXJXMXBXTXOETZXPBDNZSZCUAZYBXBXSYFAXBXSXEUBUCUDZETZXDYGJKZBDNZSZYFX
      BXSYHYJXSYHRXBXRYHXIXRUBETZYHUEXEUBUFUGZUHUIXBXRXIYJXBXRXIYJRXBXRSZXDDTZX
      HRZBUJYOYIRZBUJXIYJYNYPYQBYNYOXHYIXBXRYOXHYIRZRXBYOXRYRXBYOXDETZXRYRRDEXD
      UKYSXRYRYSXRSZXFYIXGYSXRXFYIRZYSXRUVAYSXRXFXRYIYSXRXFXRYIYTXFXEYGJKZYIXRY
      SXRYHSXFUVBSYIRZXRYSXRYHUVCXDXEYGULUMXRYHYMUNUOXRXEUPUCUDZYGJKZUVBYLXRUVE
      UEUPETZYLXRUVEUQUVFYLXRPUPUBJKUVEURUPUBXEUSUTVAVBXRUVDXEYGJXRXEVCTUVDXEVD
      XEVEXEVFVGVHVIZVJVKVLVMVNXRXGYIRZYSXRUVBUVHUVGXGYIUVBXDXEYGJVOVPVGVQVRVSV
      TWAVNWBWCXHBDWDYIBDWDWEVSWFWGYEYKCYGXEUBUCWHXOYGVDZYCYHYDYJXOYGEWIUVIXPYI
      BDXOYGXDJWJWKWLWMVTWNYEYACACALZYCXRYDXLXOXEEWIUVJXPXFBDXOXEXDJWJWKWLWOWPX
      IAEWQXLAEWQWEUHWRXBXCXJWSXBXCXMWSWTABCDXAVG $.
      $([19-Jan-97]$)
  $}

  ${
    $d x y z A $.
    sup3i.1 $e |- ( A (_ RR /\ -. A = (/) /\ E. x e. RR A. y e. A y <_ x ) $.
    $( A version of the completeness axiom for reals. $)
    sup3i $p |- E. x e. RR ( A. y e. A -. x < y /\
                  A. y e. RR ( y < x -> E. z e. A y < z ) ) $=
      ( cr wss c0 wne cv clt wbr weq wo wral wrex w3a wn wi wa wceq cle wb
      df-ne bicom a1i wcel ssel leloet exp comm syl9 imp31 biraldva birexdva
      bi2and pm5.32i 3anass 3bitr4 mpbi sup2 ax-mp ) DFGZDHIZBJZAJZKLZBAMNZBDOZ
      AFPZQZVFVEKLRBDOVGVECJKLCDPSBFOTAFPVCDHUARZVEVFUBLZBDOZAFPZQZVKEVCVLVOTZT
      VCVDVJTZTVPVKVCVQVRVCVLVDVOVJVLVDUCVCVDVLDHUDUEUFVCVNVIAFVCVFFUGZTVMVHBDV
      CVSVEDUGZVMVHUCZVCVTVEFUGZVSWADFVEUHWBVSWAWBVSWAVEVFUIUJUKULUMUNUOUPUQVCV
      LVOURVCVDVJURUSUTABCDVAVB $.
      $([23-Aug-99]$)

    $( Closure of supremum of a non-empty bounded set of reals. $)
    suprcl $p |- sup ( A , RR , < ) e. RR $=
      ( vz cr clt ltso sup3i supcl ) ABEFCGHABECDIJ $.
      $([12-Sep-99]$)

    $( A member of a non-empty bounded set of reals is less than or equal
       to the set's upper bound. $)
    suprub $p |- ( B e. A -> B <_ sup ( A , RR , < ) ) $=
      ( vz wcel cr clt csup cle wbr wn ltso sup3i supub wa wb wss c0 wceq cv
      wral wrex w3a 3simp1 ax-mp ssi suprcl jctir leltt syl mpbird ) DCGZDCHIJZ
      KLZUODILMZABFHCDINABFCEOPUNDHGZUOHGZQUPUQRUNURUSCHDCHSZCTUAMZBUBAUBKLBCUC
      AHUDZUEUTEUTVAVBUFUGUHABCEUIUJDUOUKULUM $.
      $([12-Sep-99]$)
  $}

  ${
    $d x y z $.  $( ` z ` is dummy. $)
    $( The set of natural numbers is unbounded above.  Theorem I.23 of
       Apostol, p. 26. $)
    nnunb $p |- -. E. x e. RR A. y e. NN ( y < x \/ y = x ) $=
      ( vz cv clt wbr weq wo cn wral cr wrex wn wi wa wcel wex pm3.24 wal cn1
      cmin co oprex wceq eleq1 breq1 birexdv bi2imd cla4v caddc cn0 ax1re
      ax0re w3a lt01 axltadd mpi mp3an1 mpan cc recnt ax0id syl breq1d mpbid
      wb ltsubaddt mp3an2 anabss mpbird syl7 subrclt mpan2 syl5 pm2.43d df-rex
      syl6ib comm nnret sylan5 exp pm5.32d biexdv peano2c imran breq2 bi2and
      cla4ev 19.23aiv syl6bi syld df-ral syl5ib alnex bitr2 bicon1 syl6ibr
      imland mtoi imnan mpbi nex mtbir c0 wne peano1c noel2 ax-mp df-ne mpbir
      wss nnssre sup2 mto ) BDZADZEFZBAGHBIJAKLZXPXOEFZMZBIJZXQXOCDZEFZCILZNZBK
      JZOZAKLZYHXPKPZYGOZAQYJAYIYGMNYJMYIYGYAYAMZOYARYIYFYKYAYIYFXOIPZXSOZBQZYK
      YIXOKPZYENZBSZYNYFYIYQYBIPZXPTUAUBZYBEFZOZCQZYNYQYIUVBYQYIYTCILZUVBYQYIUV
      CYQYSKPZYIUVCNYIYQUVDYSXPEFZUVCYIYPUVDUVEUVCNZNBYSXPTUAUCXOYSUDZYOUVDYEUV
      FXOYSKUEUVGXQUVEYDUVCXOYSXPEUFUVGYCYTCIXOYSYBEUFUGUHUHUIYIUVEXPXPTUJUBZEF
      ZYIXPUKUJUBZUVHEFZUVITKPZYIUVKULUKKPZUVLYIUVKUMUVMUVLYIUNUKTEFUVKUOUKTXPU
      PUQURUSYIUVJXPUVHEYIXPUTPUVJXPUDXPVAXPVBVCVDVEYIUVEUVIVFZYIUVLYIUVNULXPTX
      PVGVHVIVJVKYIUVLUVDULXPTVLVMVNVOYTCIVPVQVRYIUVBYRXPYBTUJUBZEFZOZCQYNYIUVA
      UVQCYIYRYTUVPYIYRYTUVPVFZYIYBKPZUVRYRYIUVLUVSUVRULXPTYBVGVHYBVSVTWAWBWCUV
      QYNCUVQUVOIPZUVPOZYNYRUVTUVPYBWDWEYMUWABUVOYBTUJUCXOUVOUDYLUVTXSUVPXOUVOI
      UEXOUVOXPEWFWGWHVCWIWJWKYEBKWLWMYNYAYAYLXTNBSYNMXTBIWLYLXSBWNWOWPWQWRWSYI
      YGWTXAXBYGAKVPXCIXDXEZXRYHUWBIXDUDMZTIPUWCXFITXGXHIXDXIXJIKXKUWBXRYHXLABC
      IXMURUSXN $.
      $([21-Jan-97]$)
  $}

  ${
    $d x y $.
    $( Archimedean property of real numbers.  For any real number, there is an
       integer greater than it.  Theorem I.29 of Apostol,
       p. 26. $)
    arch $p |- A. x e. RR E. y e. NN x < y $=
      ( cv clt wbr weq wo cn wral wn cr wrex nnunb ralnex mpbir wcel wi wa wb
      axlttri nnret sylan5 eqcomb biror orcom bitr bineg syl6bb biimprd exp
      r19.21aiv r19.22 syl rexnal syl5ibr r19.20ia ax-mp ) BCZACZDEZBAFZGZBHIZJ
      ZAKIZUSURDEZBHLZAKIVEVCAKLJABMVCAKNOVDVGAKUSKPZVBJZBHLZVGVDVHVIVFQZBHIVJV
      GQVHVKBHVHURHPZVKVHVLRZVFVIVMVFABFZUTGZJZVIVHURKPVFVPSVLUSURTURUAUBVOVBVO
      VAUTGVBVNVAUTABUCUDVAUTUEUFUGUHUIUJUKVIVFBHULUMVBBHUNUOUPUQ $.
      $([21-Jan-97]$)
  $}


