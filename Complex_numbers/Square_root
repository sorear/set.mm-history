$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Square root
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c sqr $. $( Positive square root of a positive real number. $)

  $( Extend class notation to include positive square root of a positive real
     number. $)
  csqr $a class sqr $.

  ${
    $d x y z $.
    $( Define a function whose value is the positive square root of a positive
       real number.  The square root of ` x ` is the supremum of all reals
       whose square is less than ` x ` . $)
    df-sqr $a |- sqr = { <. x , y >. | ( ( x e. RR /\ 0 _10 <_ x ) /\
     y = sup ( { z e. RR | ( 0 _10 <_ z /\ ( z x. z ) <_ x ) } , RR , < ) ) } $.
  $}

  ${
    $d x y z w A $.  $( ` y z w ` are dummy. $)
    $( Value of square root function. $)
    sqrval $p |- ( ( A e. RR /\ 0 _10 <_ A ) -> ( sqr ` A ) =
          sup ( { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } , RR , < ) ) $=
      ( vw vy vz cr wcel cn0 cle wbr wa cv crab csqr cfv cmulc co clt csup
      wceq breq2 rabcl ltsor supex biland rbiabdv supeq1 syl copab df-sqr
      biran biopab eqtr4 opabval2 sylbir ) BFGHBIJZKBHCLZIJZCFMZGBNOHALZIJZUTUT
      PQZBIJZKZAFMZFRSZTURUPCBFUQBHIUAUBDEBVAVBDLZIJZKZAFMZFRSZVFUSNFVERUCUDVGB
      TZVJVETVKVFTVLVIVDAFVLVHVCVAVGBVBIUAUEUFFVJVERUGUHNVGFGHVGIJZKZELVKTZKZDE
      UIVGUSGZVOKZDEUIDEAUJVRVPDEVQVNVOURVMCVGFUQVGHIUAUBUKULUMUNUO $.
      $([23-May-99]$)
  $}

  $( Square root of zero. $)
  sqr0 $p |- ( sqr ` 0 _10 ) = 0 _10 $=
    ( vx cn0 csqr cfv csn cr clt csup cv cle wbr cmulc co wa crab wcel wceq
    sqrval ax0re cleqid eqle ax-mp mp2an cab wn wb axmulrcl anabss jctir leltt
    syl ltsqt pm2.15d sylbid adantld imp eleq1 breq2 opreq12 breq1d bi2and 0cn
    mulzer1 mulrcl pm3.2i mpbiri impbi biab df-rab df-sn 3eqtr4 supeq1 eqtr
    ltso supsn ) BCDZBEZFGHZBVPBAIZJKZVSVSLMZBJKZNZAFOZFGHZVRBFPZBBJKZVPWEQABRS
    BBQWGBTBBSSUAUBZUCWDVQQWEVRQVSFPZWCNZAUDVSBQZAUDWDVQWJWKAWJWKWIWCWKWIWBWKVT
    WIWBBWAGKZUEZWKWIWAFPZWFNWBWMUFWIWNWFWIWNVSVSUGUHSUIWABUJUKWIWKWLVSULUMUNUO
    UPWKWJWFWGBBLMZBJKZNZNWKWIWFWCWQVSBFUQWKVTWGWBWPVSBBJURWKWAWOBJWKWAWOQVSBVS
    BLUSUHUTVAVAWFWQSWGWPWHWOBQWPBVBVCWOBBBSSVDSUAUBVEVEVFVGVHWCAFVIABVJVKFWDVQ
    GVLUBVMWFVRBQSFBGVNVOUBVM $.
    $([23-May-99]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    sqrlem1.1 $e |- A e. RR $.
    sqrlem1.2 $e |- 0 _10 < A $.

    $( Lemma for square root theorem. $)
    sqrlem1 $p |- A < ( ( 1 _10 + A ) x. ( 1 _10 + A ) ) $=
      ( cn1 caddc co clt wbr cmulc ax1re addrcl mulrcl lttr cn0 recn addid2
      ax0re ltadd1 lt01 mpbi eqbrtrr mulid2 addgt0i ltmul1i 1cn addid1 ltadd2
      mp2an ) ADAEFZGHUIUIUIIFZGHAUJGHAUIUJBDAJBKZUIUIUKUKLMNAEFZAUIGAABOPNDGHU
      LUIGHNDAQJBRSTUADUIIFZUIUJGUIUIUKOUBDUIGHUMUJGHDUIUIJUKUKDAJBSCUCUDDNEFZD
      UIGDUEUFNAGHUNUIGHNADQBJUGCTUATUAUH $.
      $([16-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem2 $p |- ( ( A / ( 1 _10 + A ) ) x. ( A / ( 1 _10 + A ) ) ) < A $=
      ( cn1 caddc co cdiv cmulc clt wbr recn ax1re addrcl lt01 addgt0i ltne0
      divmuldiv mulrcl cn0 ax0re lttr sqrlem1 mp2an divass divid opreq2i eqtr
      mulid1 eqtr2 breq12i ltdivi wb ltmul2 ax-mp mpbi mpbir ) ADAEFZGFZURHFZAI
      JAAHFZUQUQHFZGFZAVAHFZVAGFZIJZUSVBAVDIAUQAUQABKZUQDALBMZKZVFVHUQVGDALBNCO
      PZVIQVDADHFZAVDAVAVAGFZHFVJAVAVAVFVAUQUQVGVGRZKZVMVAVLSAIJZAVAIJZSVAIJSAV
      ATBVLUACABCUBZUCZPZUDVKDAHVAVMVRUEUFUGAVFUHUIUJUTVCIJZVEUTVCVAAABBRAVABVL
      RVLVQUKVOVSVNVOVSULCAVAABVLBUMUNVPUOUOUP $.
      $([16-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem3 $p |- 0 _10 < ( A / ( 1 _10 + A ) ) $=
      ( cn1 caddc co ax1re addrcl lt01 addgt0i divgt0i ) ADAEFBDAGBHCDAGBICJK
      $.
      $([16-May-99]$)

    ${
      $d x y A $.  $d x y S $.  $d x D $.
      sqrlem4.3 $e |- S = { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } $.
      $( Lemma for square root theorem. $)
      sqrlem4 $p |- ( D e. S <->
                    ( D e. RR /\ ( 0 _10 <_ D /\ ( D x. D ) <_ A ) ) ) $=
        ( wcel cn0 cv cle wbr cmulc co wa cr crab eleq2i wceq breq2 opreq12
        anabss breq1d bi2and rabcl bitr ) CDHCIAJZKLZUGUGMNZBKLZOZAPQZHCPHICKLZ
        CCMNZBKLZOZODULCGRUKUPACPUGCSZUHUMUJUOUGCIKTUQUIUNBKUQUIUNSUGCUGCMUAUBU
        CUDUEUF $.
        $([21-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem5 $p |- ( ( D e. RR /\ ( 0 _10 < D /\ ( D x. D ) < A ) ) ->
                        D e. S ) $=
        ( cr wcel cn0 clt wbr cmulc co wa cle wi wceq wo leloet orc syl5bir
        ax0re mpan axmulrcl anabss sylan mpan2 im2and imdistani sqrlem4 sylibr
        ) CHIZJCKLZCCMNZBKLZOZOUMJCPLZUOBPLZOZOCDIUMUQUTUMUNURUPUSJHIZUMUNURQVA
        UMOURUNJCRZSUNJCTUNVBUAUBUCUDUMBHIZUPUSQZUOHIZVCVDUMVEVCOUSUPUOBRZSUPUO
        BTUPVFUAUBUMVECCUEUFUGEUHUIUJABCDEFGUKUL $.
        $([23-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem6 $p |- ( S (_ RR /\ S =/= (/) /\
                     E. x e. RR A. y e. S y < x ) $=
        ( cr wss c0 wne cv clt wbr wral wrex cn0 cle cmulc co wa crab ssrab
        eqsstr wceq wn df-ne wcel sqrlem4 ax0re cleqid wo olc leloe sylibr
        ax-mp 0cn mulzer1 ltlei eqbrtr pm3.2i mpbir noel2 cn1 caddc breq2
        biraldv rcla4ev ax1re addrcl wi axmulrcl anabss leloet axlttrn mulrcl
        mp3an3 sqrlem1 jctr syl5 breq1 mpbiri a1i jaod sylbid mpan2 syl wb
        mpan cded opreq12 breq1d bi2imd elimel lt2sq ltle syl2an lt01 addgt0i
        biimprd dedth mpbii a1d com23 syld imp32 sylbi rgen mp2an 3pm3.2i ) DHI
        DJKZBLZALZMNZBDOZAHPZDQXMRNXMXMSTCRNUAZAHUBHGXQAHUCUDXKDJUEUFZDJUGQDUHZ
        XRXSQHUHZQQRNZQQSTZCRNZUAZUAACQDEFGUIXTYDUJYAYCQQUEZYAQUKYEQQMNZYEULYAY
        EYFUMQQUJUJUNUOUPYBQCRQUQURQCUJEFUSUTVAVAVBDQVCUPVBVDCVETZHUHXLYGMNZBDO
        ZXPXOYIAYGHXMYGUEXNYHBDXMYGXLMVFVGVHVDCVIEVJZYHBDXLDUHXLHUHZQXLRNZXLXLS
        TZCRNZUAUAYHACXLDEFGUIYKYLYNYHYKYNYLYHYKYNYMYGYGSTZMNZYLYHVKYKYMHUHZYNY
        PVKZYKYQXLXLVLVMYQCHUHZYRYQYSUAZYNYMCMNZYMCUEZULYPYMCVNYTUVAYPUVBYTUVAC
        YOMNZUAZYPUVAYQYSYOHUHUVDYPVKYMCYOVOYGYGYJYJVPVQUVAUVCCEFVRZVSVTUVBYPVK
        YTUVBYPUVCYMCYOMWAUVEWBWCWDWEEWFWGYKYLYPYHYKYLQXLMNZQXLUEZULZYPYHVKZXTY
        KYLUVHWHQXLVNUJWIYKUVFUVIUVGYKUVFUVIVKQYKXLVDWJZMNZUVJUVJSTZYOMNZUVJYGM
        NZVKZVKXLVDXLUVJUEZUVFUVKUVIUVOXLUVJQMVFUVPYPUVMYHUVNUVPYMUVLYOMUVPYMUV
        LUEXLUVJXLUVJSWKVMWLXLUVJYGMWAWMWMUVKUVNUVMUVKQYGMNZUVNUVMWHZQUVJRNQYGR
        NUVRUVKUVQUVJYGXLVDHVIWNZYJWOQUVJUJUVSWPQYGUJYJWPWQVDCVIEWRFWSZWFWTXAUV
        GUVIVKYKUVGYHYPUVGUVQYHQXLYGMWAUVTXBXCWCWDWEXDXEXDXFXGXHXIXJ $.
        $([21-May-99]$)

      ${
        $d x y z S $.  $d z A $.
        sqrlem7.4 $e |- B = sup ( S , RR , < ) $.
        $( Lemma for square root theorem. $)
        sqrlem7 $p |- B e. RR $=
          ( vy vz cr wcel clt csup eleq1i ltsor wss c0 wne cv wbr wral wrex
          w3a wn wi wa sqrlem6 axsup ax-mp supcl mpbir ) CKLDKMNZKLCUMKHOAIJKDM
          PDKQDRSITZATZMUAZIDUBAKUCUDUOUNMUAUEIDUBUPUNJTMUAJDUCUFIKUBUGAKUCAIBD
          EFGUHAIJDUIUJUKUL $.
          $([24-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem8 $p |- 0 _10 < B $=
          ( vy vz cn0 cr clt csup cn1 caddc co cdiv wbr cle ax0re ax1re addrcl
          lt01 addgt0i ltne0 divrcl wcel eleq1i sqrlem7 mpbi ltletr sqrlem3 wn
          lelt cmulc wa sqrlem5 sqrlem2 pm3.2i mp2an ltsor wss c0 wne cv wral
          wrex w3a wi sqrlem6 axsup ax-mp supub mpbir breqtrr ) KDLMNZCMKBOBPQZ
          RQZMSZVSVQTSZKVQMSKVSVQUABVREOBUBEUCZVRWBOBUBEUDFUEUFUGZCLUHVQLUHCVQL
          HUIABCDEFGHUJUKZULBEFUMZWAVQVSMSUNZVSVQWCWDUOVSDUHZWFVSLUHVTVSVSUPQBM
          SZUQWGABVSDEFGURWCVTWHWEBEFUSUTVAAIJLDVSMVBDLVCDVDVEIVFZAVFZMSZIDVGAL
          VHVIWJWIMSUNIDVGWKWIJVFMSJDVHVJILVGUQALVHAIBDEFGVKAIJDVLVMVNVMVOVAHVP
          $.
          $([24-May-99]$)

      $}
    $}

    ${
      sqrlem9.3 $e |- B e. RR $.
      sqrlem9.4 $e |- C e. RR $.
      sqrlem9.5 $e |- 0 _10 < B $.
      sqrlem9.6 $e |- A < ( B x. B ) $.
      sqrlem9.7 $e |- C = ( ( B + ( A / B ) ) / ( 1 _10 + 1 _10 ) ) $.
      $( Lemma for square root theorem. $)
      sqrlem9 $p |- 0 _10 < C $=
        ( cn0 cdiv co caddc cn1 clt ltne0 divrcl addrcl ax1re divgt0i addgt0i
        lt01 breqtrr ) KBABLMZNMZOONMZLMCPUFUGBUEFABDFBFHQRZSOOTTSBUEFUHHABDFEH
        UAUBOOTTUCUCUBUAJUD $.
        $([17-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem10 $p |- C < B $=
        ( cdiv co caddc cn1 clt cmulc wbr ltne0 divrcl addrcl ax1re mulrcl
        lt01 addgt0i ltdivi ltadd2 mpbi recn divass divid opreq2i mulid1 3eqtr
        breqtr 1cn addcl mulcom 1p1times eqtr breqtrr eqbrtr ) CBABKLZMLZNNMLZK
        LZBOJVEBVDPLZVDKLZBOVCVFOQVEVGOQVCVFVDBVBFABDFBFHRZSZTBVDFNNUAUATZUBVJN
        NUAUAUCUCUDZUEVCBBMLZVFOVBBOQVCVLOQVBBBVIFFUFVBBBPLZBKLZBOAVMOQVBVNOQAV
        MBDBBFFUBFHUEIUGVNBBBKLZPLBNPLZBBBBBFUHZVQVQVHUIVONBPBVQVHUJUKBVQULZUMU
        NUGVFVDBPLVLBVDVQNNUOUOUPZUQBVQURUSUTUGVGBVDVDKLZPLVPBBVDVDVQVSVSVDVJVK
        RZUIVTNBPVDVSWAUJUKVRUMUNVA $.
        $([17-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem11 $p |- A < ( C x. C ) $=
        ( cn1 caddc co cmulc cdiv clt wbr ax1re addrcl mulrcl recn lt01
        addgt0i mulgt0i ltne0 divcan4 opreq12i divcl addcl 1cn divmuldiv eqtr2
        breq12i divrcl ltdivi cneg mulcl mulcom mulass 1p1times 3eqtr muladd
        cleqcomi mul2neg opreq1i eqtr4 divcan2 opreq2i eqtr3 eqtr negrcl
        ltadd1 negcl muln0 divcan3 divdistr cn0 cmin mul12 mulneg1 negeqi
        df-neg ax0re ltsubadd wceq wn wne df-ne negid mpbi eqbrtrr sqgt0 ax-mp
        breqtr mpbir eqbrtr ) AKKLMZWQNMZNMZWROMZBABOMZLMZXBNMZWROMZPQZACCNMZPQ
        WTAXDXFPWRAWRWQWQKKRRSZXGTZUAADUAZWRXHWQWQXGXGKKRRUBUBUCZXJUDZUEUFXFXBW
        QOMZXLNMXDCXLCXLNJJUGXBWQXBWQBXABFUAZABXIXMBFHUEZUHZUIZKKUJUJUIZXPXQWQX
        GXJUEZXRUKULUMWSXCPQZXEWSXCWRAWRDXHTXBXBBXAFABDFXNUNSZXTTXHXKUOXSWQANMZ
        YALMZBBNMZYCNMZYCOMZAUPZYFNMZYCOMZLMZYALMZPQZWSYBXCYJPWSWRANMWQYANMYBAW
        RXIWQWQXQXQUQURWQWQAXQXQXIUSYAWQAXQXIUQUTVAXCYCXAXANMZLMZBXANMZYNLMZLMY
        JBXABXAXMXOXMXOVBYMYIYOYALYCYEYLYHLYEYCYCYCYCBBFFTZUAZYQYCYPBBFFHHUDZUE
        ZUFVCYLAANMZYCOMYHABABXIXMXIXMXNXNUKYGYTYCOAAXIXIVDVEVFUGWQYNNMYOYAYNBX
        AXMXOUQUTYNAWQNBAXMXIXNVGVHVIUGVJUMYAYIPQZYKYAYIYAWQAXGDTZYEYHYDYCYCYCY
        PYPTZYPYSUNYGYCYFYFADVKZUVDTZYPYSUNSUVBVLYCWQUPZYFNMZNMZYCOMZYDYGLMZYCO
        MZPQZUVAUVIYAUVKYIPUVIUVGYAYCUVGYQUVFYFWQXQVMZYFUVDUAZUQBBXMXMXNXNVNVOW
        QAXQXIVDVJYDYGYCYDUVCUAYGUVEUAYQYSVPUMUVHUVJPQUVLUVHUVJYCYCUVGYPUVFYFWQ
        XGVKUVDTTYDYGUVCUVESZYPYRUOUVHVQYCYFNMZUVPLMZVRMZUVJPUVHUVFUVPNMWQUVPNM
        ZUPZUVRYCUVFYFYQUVMUVNVSWQUVPXQUVPYCYFYPUVDTZUAZVTUVTUVQUPUVRUVSUVQUVPU
        WBUTWAUVQWBVJVAUVRUVJPQVQUVJUVQLMZPQVQUVQUVJWCUVPUVPUWAUWASUVOWDVQYCYFL
        MZUWDNMZUWCPUWDVQWEWFZVQUWEPQUWDVQWGUWFUWDVQWHUWDYCYFYPUVDSZAYFLMZVQUWD
        PAXIWIAYCPQUWHUWDPQAYCYFDYPUVDVLIWJWKUEWJUWDUWGWLWMYCYFYCYFYQUVNYQUVNVB
        WNWOWPWJWJWJWOWJWJ $.
        $([18-May-99]$)

      ${
        $d x y A $.  $d x y z S $.  $d y z C $.  $d x D $.
        sqrlem12.8 $e |- S = { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } $.

        $( Lemma for square root theorem. $)
        sqrlem12 $p |- ( D e. S -> D < C ) $=
          ( wcel cn0 clt wbr wceq wo wa cmulc co wb cr wi cle sqrlem4 pm3.26bd
          cded breq2 birand breq1 opreq12 anabss breq1d bi2bid bi2imd ax0re
          elimel lt2sq ltle syl2an dedth syl sqrlem9 mpan2i imp axmulrcl
          lelttrt mulrcl mp3an3 mpan2 sqrlem11 pm3.27bd pm3.27d mpd adantr
          mpbird exp mpbii a1i jaod leloet mpan pm3.26d mpbid ) EFOZPEQRZPESZTZ
          EDQRZWHWIWLWJWHWIWLWHWIUAWLEEUBUCZDDUBUCZQRZWHWIWLWOUDZWHWIPDQRZWPWHE
          UEOZWIWQUAZWPUFZWHWRPEUGRZWMBUGRZUAZABEFGHNUHZUIZWRWTPWREPUJZQRZWQUAZ
          XFDQRZXFXFUBUCZWNQRZUDZUFEPEXFSZWSXHWPXLXMWIXGWQEXFPQUKULXMWLXIWOXKEX
          FDQUMXMWMXJWNQXMWMXJSEXFEXFUBUNUOUPUQURPXFUGRPDUGRXLXGWQXFDEPUEUSUTZJ
          VAPXFUSXNVBPDUSJVBVCVDVEBCDGHIJKLMVFZVGVHWHWOWIWHXBWOWHXBBWNQRZWOWHWM
          UEOZXBXPUAWOUFZWHWRXQXEWRXQEEVIUOVEXQBUEOZXRXQXSWNUEOXRWMBWNVJDDJJVKV
          LGVMVEBCDGHIJKLMVNVGWHXAXBWHWRXCXDVOZVPVQVRVSVTWJWLUFWHWJWQWLPEDQUMXO
          WAWBWCWHXAWKWHWRXAWKUDZXEPUEOWRYAPEWDUSWEVEWHXAXBXTWFWGVQ $.
          $([23-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem13 $p |- ( B = sup ( S , RR , < ) -> -. C < B ) $=
          ( vz vy cr clt csup wceq wbr wn breq2 binegd wcel cv wral ltso wss
          c0 wne wrex w3a wi wa sqrlem6 axsup ax-mp supnub wo wb cn0 cle cmulc
          co sqrlem4 pm3.26bd axlttri mpan2 syl sqrlem12 mpbid ioran sylib
          pm3.27d rgen mp2an mpbiri ) CEPQRZSZDCQTZUADVRQTZUAZVSVTWACVRDQUBUCDP
          UDZDNUEZQTZUAZNEUFWBAONPEDQUGEPUHEUIUJOUEZAUEZQTZOEUFAPUKULWHWGQTUAOE
          UFWIWGWDQTNEUKUMOPUFUNAPUKAOBEFGMUOAONEUPUQURIWFNEWDEUDZWDDSZUAZWFWJW
          KWEUSUAZWLWFUNWJWDDQTZWMWJWDPUDZWNWMUTZWJWOVAWDVBTWDWDVCVDBVBTUNABWDE
          FGMVEVFWOWCWPWDDVGIVHVIABCDWDEFGHIJKLMVJVKWKWEVLVMVNVOVPVQ $.
          $([25-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem14 $p |- -. B = sup ( S , RR , < ) $=
          ( cr clt csup wceq wbr sqrlem13 sqrlem10 mt2 ) CENOPQDCORABCDEFGHIJKL
          MSBCDFGHIJKLTUA $.
          $([25-May-99]$)
      $}

    $}

    ${
      sqrlem15.3 $e |- B e. RR $.
      sqrlem15.4 $e |- 0 _10 < B $.

      ${
        sqrlem15.5 $e |- C e. RR $.
        sqrlem15.6 $e |- 0 _10 < C $.
        $( Lemma for square root theorem. $)
        sqrlem15 $p |- B < ( B + C ) $=
          ( cn0 caddc co clt recn addid1 wbr ax0re ltadd2 mpbi eqbrtrr ) BJKLZB
          BCKLZMBBFNOJCMPUAUBMPJCBQHFRIST $.
          $([24-May-99]$)

        ${
          sqrlem16.7 $e |- C < B $.
          $( Lemma for square root theorem. $)
          sqrlem16 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 _10 + 1 _10 ) + 1 _10 ) x. B ) ) ->
                          ( ( B + C ) x. ( B + C ) ) < A ) $=
            ( cmulc co cmin cn1 caddc cdiv clt wbr 1cn addcl ax1re recn mulass
            mulcl mulrcl subrcl addrcl lt01 addgt0i ltne0 muln0 divcan2
            breq12i cn0 wb mulgt0i divrcl ltmul2 ax-mp ltaddsub 3bitr4 lttr
            muladd addcom distr2 1p1times mulid2 mulcom eqtr opreq12i 3eqtr
            opreq1i addass 3eqtr4 ltadd1 ltadd2 mpbi mpbir mpan sylbi ) CABBKLZ
            MLZNNOLZNOLZBKLZPLZQRZWDBCKLZKLZWAOLZAQRZBCOLZWLKLZAQRZWECKLZWEWFKL
            ZQRZWIWBQRWGWKWOWIWPWBQWDBCWCNNNSSTZNUAUBTZBFUBZCHUBZUCWEWBWDBWSWTU
            DWBAWADBBFFUEZUFZUBWDBWDWCNNNUAUAUGZUAUGZUBWTWDXEWCNXDUANNUAUAUHUHU
            IUHUIZUJBFGUJUKZULUMUNWEQRWGWQUOWDBXEFXFGUPCWFWEHWBWEXCWDBXEFUEZXGU
            QXHURUSWIWAAWDWHXEBCFHUEZUEZXBDUTVAWMWJQRZWKWNWMWJAWLWLBCFHUGZXLUEW
            IWAXJXBUGDVBXKWACCKLZOLZWHWHOLZOLZWACBKLZOLZXOOLZQRZWMXPWJXSQBCBCWT
            XAWTXAVCXQXOOLZWAOLWAYAOLWJXSYAWAYAXQXOCBHFUEZWHWHXIXIUGZUGUBWAXBUB
            ZVDWIYAWAOWIWCWHKLZNWHKLZOLXOXQOLYAWCNWHWRSWHXIUBZVEYEXOYFXQOWHYGVF
            YFWHXQWHYGVGBCWTXAVHVIVJXOXQXOYCUBZXQYBUBZVDVKVLWAXQXOYDYIYHVMVNUMX
            NXRQRZXTXNXRXOWAXMXBCCHHUEZUGWAXQXBYBUGYCVOXMXQQRZYJXMXQWAYKYBXBVPC
            BQRZYLUNCQRYMYLUOICBCHFHURUSJVQVQVQVRVSVT $.
            $([26-May-99]$)

          ${
            $d x y A $.  $d x y z B $.  $d x y z C $.  $d x y z S $.
            sqrlem17.8 $e |- S =
                             { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } $.
            $( Lemma for square root theorem. $)
            sqrlem17 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 _10 + 1 _10 ) + 1 _10 ) x. B ) ) ->
                          ( B + C ) e. S ) $=
              ( cmulc co cmin cn1 caddc cdiv clt wbr cn0 wa wcel sqrlem16
              addgt0i jctil cr sqrlem5 addrcl mpan syl ) DBCCNOPOQQROQROCNOSOTU
              AZUBCDROZTUAZUNUNNOBTUAZUCZUNEUDZUMUPUOBCDFGHIJKLUECDHJIKUFUGUNUH
              UDUQURABUNEFGMUICDHJUJUKUL $.
              $([26-May-99]$)

            $( Lemma for square root theorem. $)
            sqrlem18 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1 _10 + 1 _10 ) + 1 _10 ) x. B ) ) ->
                          -. B = sup ( S , RR , < ) ) $=
              ( vy vz cmulc co cmin cn1 caddc cdiv clt wbr cr csup wceq wn
              breq1 binegd wcel sqrlem17 ltsor wss c0 wne cv wral wrex w3a wi
              wa sqrlem6 axsup ax-mp supub syl syl5bir comm sqrlem15 mt2i ) DBC
              CPQRQSSTQSTQCPQUAQUBUCZCEUDUBUEZUFZCCDTQZUBUCZVMVKVOUGZVMVPVLVNUB
              UCZUGZVKVMVOVQCVLVNUBUHUIVKVNEUJVRABCDEFGHIJKLMUKANOUDEVNUBULEUDU
              MEUNUONUPZAUPZUBUCZNEUQAUDURUSVTVSUBUCUGNEUQWAVSOUPUBUCOEURUTNUDU
              QVAAUDURANBEFGMVBANOEVCVDVEVFVGVHBCDFGHIJKVIVJ $.
              $([26-May-99]$)

          $}
        $}
      $}

      ${
        sqrlem19.5 $e |- ( B x. B ) < A $.
        $( Lemma for square root theorem. $)
        sqrlem19 $p |- 0 _10 < ( ( A - ( B x. B ) ) /
                      ( ( ( 1 _10 + 1 _10 ) + 1 _10 ) x. B ) ) $=
          ( cmulc co cmin cn1 caddc mulrcl subrcl ax1re addrcl cn0 clt wbr
          ax0re ltaddsub recn addid2 eqbrtr mpbi lt01 addgt0i mulgt0i divgt0i
          ) ABBHIZJIZKKLIZKLIZBHIAUJCBBEEMZNUMBULKKKOOPZOPZEMQUJLIZARSQUKRSQUJA
          TUNCUAUQUJARUJUJUNUBUCGUDUEUMBUPEULKUOOKKOOUFUFUGUFUGFUHUI $.
          $([19-May-99]$)

        ${
          $d x y w A $.  $d x y z w B $.  $d x y z w S $.
          sqrlem20.6 $e |- S =
                             { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } $.
          $( Lemma for square root theorem. $)
          sqrlem20 $p |- -. B = sup ( S , RR , < ) $=
            ( vw cn0 cv clt wbr cmulc co cmin cn1 caddc cdiv wa cr wrex csup
            wceq wn mulrcl subrcl ax1re addrcl recn lt01 addgt0i ltne0 muln0
            divrcl sqrlem19 posex wcel wi w3a cded breq1 birimd eleq1 breq2
            bi3and divgt0i halfpos mpbi 3pm3.2i elimhyp 3simp1 ax-mp 3simp2
            3simp3 sqrlem18 dedth 3exp imp4d r19.23aiv ) LKMZNOZWCCNOZWCBCCPQZR
            QZSSTQZSTQZCPQZUAQZNOZUBUBZKUCUDCDUCNUEUFUGZKCWKGWGWJBWFECCGGUHUIWI
            CWHSSSUJUJUKZUJUKZGUHWICWIWPULCGULWIWPWHSWOUJSSUJUJUMUMUNZUMUNUOCGH
            UOUPUQHBCEFGHIURUSWMWNKUCWCUCUTZWDWEWLWNWRWDWEWLWNVAZWRWDWEVBZWSWTW
            CCWHUAQZVCZWKNOZWNVAWCXAWCXBUFZWLXCWNWCXBWKNVDVEABCXBDEFGHXBUCUTZLX
            BNOZXBCNOZVBZXEWTXHXAUCUTZLXANOZXACNOZVBWCXAXDWRXEWDXFWEXGWCXBUCVFW
            CXBLNVGWCXBCNVDVHXAXBUFXIXEXJXFXKXGXAXBUCVFXAXBLNVGXAXBCNVDVHXIXJXK
            CWHGWOWHWOWQUOUQCWHGWOHWQVILCNOXKCGVJHVKVLVMZXEXFXGVNVOXHXFXLXEXFXG
            VPVOXHXGXLXEXFXGVQVOJVRVSVTWAWBVO $.
            $([26-May-99]$)
        $}
      $}
    $}

    ${
      $d x y z A $.  $d x B $.  $d x S $.
      sqrlem21.3 $e |- S = { x e. RR | ( 0 _10 <_ x /\ ( x x. x ) <_ A ) } $.
      sqrlem21.4 $e |- B = sup ( S , RR , < ) $.
      $( Lemma for square root theorem. $)
      sqrlem21 $p |- -. A < ( B x. B ) $=
        ( cmulc co clt wbr cr csup wceq wn cn1 caddc cded cleq1 binegd cdiv
        wcel eleq1 sqrlem7 ax1re addrcl keephyp id opreq2 opreq12d opreq1d
        eleq1d sqrlem8 ltne0 divrcl lt01 addgt0i cn0 breq2 opreq12 anabss
        breq2d sqrlem1 elimhyp cleqid sqrlem14 dedth mt2 ) BCCIJZKLZCDMKNZOZVKV
        MPVKCQBRJZSZVLOZPCVNCVOOZVMVPCVOVLTUAABVOVOBVOUBJZRJZQQRJZUBJZDEFVKCMUC
        VNMUCVOMUCCVNCVOMUDVNVOMUDABCDEFGHUEZQBUFEUGZUHVKCBCUBJZRJZVTUBJZMUCVNB
        VNUBJZRJZVTUBJZMUCWAMUCCVNVQWFWAMVQWEVSVTUBVQCVOWDVRRVQUICVOBUBUJUKULUM
        VNVOOZWIWAMWJWHVSVTUBWJVNVOWGVRRWJUIVNVOBUBUJUKULUMWEVTCWDWBBCEWBCWBABC
        DEFGHUNZUOUPUGQQUFUFUGZVTWLQQUFUFUQUQURUOZUPWHVTVNWGWCBVNEWCVNWCQBUFEUQ
        FURZUOUPUGWLWMUPUHVKUSCKLUSVNKLUSVOKLCVNCVOUSKUTVNVOUSKUTWKWNUHVKBVOVOI
        JZKLBVNVNIJZKLCVNVQVJWOBKVQVJWOOCVOCVOIVAVBVCWJWPWOBKWJWPWOOVNVOVNVOIVA
        VBVCBEFVDVEWAVFGVGVHHVI $.
        $([24-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem22 $p |- -. ( B x. B ) < A $=
        ( cmulc co clt wbr cr csup wceq wn cn1 caddc cdiv cded cleq1 binegd
        wcel eleq1 sqrlem7 ax1re addrcl lt01 addgt0i ltne0 divrcl keephyp cn0
        breq2 sqrlem8 sqrlem3 opreq12 anabss breq1d sqrlem2 elimhyp sqrlem20
        dedth mt2 ) CCIJZBKLZCDMKNZOZVFVHPVFCBQBRJZSJZTZVGOZPCVJCVKOZVHVLCVKVGU
        AUBABVKDEFVFCMUCVJMUCVKMUCCVJCVKMUDVJVKMUDABCDEFGHUEBVIEQBUFEUGZVIVNQBU
        FEUHFUIUJUKULVFUMCKLUMVJKLUMVKKLCVJCVKUMKUNVJVKUMKUNABCDEFGHUOBEFUPULVF
        VKVKIJZBKLVJVJIJZBKLCVJVMVEVOBKVMVEVOOCVKCVKIUQURUSVJVKOZVPVOBKVQVPVOOV
        JVKVJVKIUQURUSBEFUTVAGVBVCHVD $.
        $([26-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem23 $p |- ( B x. B ) = A $=
        ( cmulc co wceq clt wbr wn wa sqrlem7 mulrcl lttri3 sqrlem22 sqrlem21
        pm3.2i mpbir ) CCIJZBKUCBLMNZBUCLMNZOUCBCCABCDEFGHPZUFQERUDUEABCDEFGHSA
        BCDEFGHTUAUB $.
        $([26-May-99]$)
    $}

    $( Lemma for square root closure. $)
    sqrlem24 $p |- ( sqr ` A ) e. RR $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup sqrval ax0re ltlei
      mp2an sqrlem7 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDOE
      DQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSEAUH
      BHAUIBCUJUKUL $.
      $([26-May-99]$)

    $( The square root of a positive real is positive. $)
    sqrgt0i $p |- 0 _10 < ( sqr ` A ) $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup sqrval ax0re ltlei
      mp2an sqrlem8 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDOE
      DQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSEAUH
      BHAUIBCUJUKUL $.
      $([26-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem26 $p |- ( ( sqr ` A ) x. ( sqr ` A ) ) = A $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup sqrval ax0re ltlei
      mp2an sqrlem23 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDO
      EDQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSEAU
      HBHAUIBCUJUKUL $.
      $([24-May-99]$)

  $}

  ${
    sqrth.1 $e |- A e. RR $.
    $( Square root theorem.  Theorem I.35 of Apostol, p. 29. $)
    sqrth $p |- ( 0 _10 <_ A -> ( ( sqr ` A ) x. ( sqr ` A ) ) = A ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv cmulc co ax0re leloe cn1 cded fveq2
      opreq12d id cleq12d cr ax1re keepel elimgt0 sqrlem26 dedth sqr0 opreq12i
      0cn mulzer1 eqtr mpbii jaoi sylbi ) CADECAFEZCAGZHAIJZUQKLZAGZCAMBNUOUSUP
      UOUSUOAOPZIJZVAKLZUTGAOAUTGZURVBAUTVCUQVAUQVAKAUTIQZVDRVCSTUTUOAOUABUBUCA
      UDUEUFUPCIJZVEKLZCGUSUPVFURCAUPVEUQVEUQKCAIQZVGRUPSTVFCCKLCVECVECKUGUGUHC
      UIUJUKULUMUN $.
      $([26-May-99]$)

    $( The square root of a non-negative real is a real. $)
    sqrcl $p |- ( 0 _10 <_ A -> ( sqr ` A ) e. RR ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv cr wcel ax0re leloe cn1 cded fveq2
      eleq1d ax1re keepel elimgt0 sqrlem24 dedth sqr0 syl5eqr mpbii jaoi sylbi
      ) CADECAFEZCAGZHAIJZKLZCAMBNUIULUJUIULUIAOPZIJZKLAOAUMGUKUNKAUMIQRUMUIAOK
      BSTAUAUBUCUJCKLULUJCUKKUJCIJUKCCAIQUDUERMUFUGUH $.
      $([26-May-99]$)

    $( The square root of a positive real is positive. $)
    sqrgt0 $p |- ( 0 _10 < A -> 0 _10 < ( sqr ` A ) ) $=
      ( cn0 clt wbr csqr cfv cn1 cded wceq fveq2 breq2d cr ax1re keepel
      elimgt0 sqrgt0i dedth ) CADEZCAFGZDECSAHIZFGZDEAHAUAJTUBCDAUAFKLUASAHMBNO
      APQR $.
      $([26-May-99]$)

    $( The square root of a non-negative real is non-negative. $)
    sqrge0 $p |- ( 0 _10 <_ A -> 0 _10 <_ ( sqr ` A ) ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv ax0re leloe cr wcel wb leloet mpan
      sqrgt0 orc syl syl5bir ltle sqrcl mpcom fveq2 sqr0 cleqcomi syl5eq
      breq2d cleqid eqle ax-mp mpbii jaoi sylbi ) CADEZCAFEZCAGZHCAIJZDEZCAKBLU
      QUTURUSMNZUQUTVAUTCUSFEZCUSGZHZUQCMNVAUTVDOCUSPKQUQVBVDABRVBVCSTUAUQUPVAC
      AKBUBABUCTUDURCCDEZUTURCUSCDURCIJZUSCCAIUEVFCUFUGUHUICCGVECUJCCKKUKULUMUN
      UO $.
      $([26-May-99]$)

    ${
      sqr11.1 $e |- B e. RR $.
      $( The square root function is one-to-one. $)
      sqr11 $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
                 ( ( sqr ` A ) = ( sqr ` B ) <-> A = B ) ) $=
        ( cn0 cle wbr wa csqr cfv wceq cmulc co opreq12 anabss adantl sqrth
        adantr 3eqtr3d exp wi fveq2 a1i impbid ) EAFGZEBFGZHZAIJZBIJZKZABKZUGUJ
        UKUGUJHUHUHLMZUIUILMZABUJULUMKZUGUJUNUHUIUHUILNOPUGULAKZUJUEUOUFACQRRUG
        UMBKZUJUFUPUEBDQPRSTUKUJUAUGABIUBUCUD $.
        $([27-Jul-99]$)

      ${
        sqrmuli.1 $e |- 0 _10 <_ A $.
        sqrmuli.2 $e |- 0 _10 <_ B $.
        $( Square root distributes over multiplication. $)
        sqrmuli $p |- ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) $=
          ( cmulc co csqr cfv wceq cn0 cle wbr wb cr wcel mulge0 mp2an mulrcl
          sqrcl ax-mp sq11 sqrge0 sqrth opreq12i recn mul4 3eqtr4r mpbi ) ABGHZ
          IJZULGHZAIJZBIJZGHZUPGHZKZULUPKZLULMNZLUPMNZURUSOULUPLUKMNZULPQLAMNZL
          BMNZVBABCDREFSZUKABCDTZUAUBUNUOVCUNPQEACUAUBZVDUOPQFBDUAUBZTUCVBUTVEU
          KVFUDUBLUNMNZLUOMNZVAUNUOVGVHRVCVIEACUDUBVDVJFBDUDUBSSUNUNGHZUOUOGHZG
          HUKUQUMVKAVLBGVCVKAKEACUEUBVDVLBKFBDUEUBUFUNUOUNUOUNVGUGZUOVHUGZVMVNU
          HVBUMUKKVEUKVFUEUBUIUJ $.
          $([30-Jul-99]$)
      $}

      $( Square root distributes over multiplication. $)
      sqrmul $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
                 ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) ) $=
        ( cn0 cle wbr cmulc co csqr cfv wceq cded opreq1 fveq2d fveq2 opreq1d
        cleq12d opreq2 opreq2d cr ax0re keepel elimge0 sqrmuli dedth2h ) EAFGZE
        BFGZABHIZJKZAJKZBJKZHIZLUGAEMZBHIZJKZUNJKZULHIZLUNUHBEMZHIZJKZUQUSJKZHI
        ZLABEEAUNLZUJUPUMURVDUIUOJAUNBHNOVDUKUQULHAUNJPQRBUSLZUPVAURVCVEUOUTJBU
        SUNHSOVEULVBUQHBUSJPTRUNUSUGAEUACUBUCUHBEUADUBUCAUDBUDUEUF $.
        $([30-Jul-99]$)

      $( Relationship between square root and squares. $)
      sqrsq $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
         ( ( sqr ` A ) = B <-> A = ( B x. B ) ) ) $=
        ( cn0 cle wbr wa csqr cfv cmulc co wceq wb sqrmul anabss sqrth eqtrd
        cleq2d adantl mulrcl sqr11 sqge0 mpan2 adantr bitr3d ) EAFGZEBFGZHAIJZB
        BKLZIJZMZUIBMZAUJMZUHULUMNUGUHUKBUIUHUKBIJZUOKLZBUHUKUPMBBDDOPBDQRSTUGU
        LUNNZUHUGEUJFGUQAUJCBBDDUAUBBDUCUDUEUF $.
        $([31-Jul-99]$)

      $( Square root is monotonic. $)
      sqrle $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
          ( A <_ B <-> ( sqr ` A ) <_ ( sqr ` B ) ) ) $=
        ( cn0 cle wbr wa csqr cfv cmulc co wb cr wcel wi le2sqt sqrcl syl2an
        sqrge0 syl2and pm2.43i sqrth breqan12d bitrd bicomd ) EAFGZEBFGZHZAIJZB
        IJZFGZABFGZUIULUJUJKLZUKUKKLZFGZUMUIULUPMZUIEUJFGZEUKFGZUQUGUHUJNOUKNOU
        RUSHUQPUGUHUJUKQACRBDRSACTBDTUAUBUGUHUNAUOBFACUCBDUCUDUEUF $.
        $([3-Aug-99]$)

    $}

    $( Square root of square. $)
    sqrsqid $p |- ( 0 _10 <_ A -> ( sqr ` ( A x. A ) ) = A ) $=
      ( cn0 cle wbr cmulc co csqr cfv wceq wa mulrcl sqrsq cleqid mpbiri sqge0
      a1i sylan anabss ) CADEZAAFGZHIAJZCUADEZTUBTUCTKUBUAUAJUAAAABBLBMUANOUCTA
      BPQRS $.
      $([2-Aug-99]$)
  $}

  $( The square root of a non-negative real is a real. $)
  sqrclt $p |- ( ( A e. RR /\ 0 _10 <_ A ) -> ( sqr ` A ) e. RR ) $=
    ( cr wcel cn0 cle wbr csqr cfv wi cded wceq breq2 fveq2 eleq1d bi2imd
    ax0re elimel sqrcl dedth imp ) ABCZDAEFZAGHZBCZUAUBUDIDUAADJZEFZUEGHZBCZIAD
    AUEKZUBUFUDUHAUEDELUIUCUGBAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( The square root of a positive real is positive. $)
  sqrgt0t $p |- ( ( A e. RR /\ 0 _10 < A ) -> 0 _10 < ( sqr ` A ) ) $=
    ( cr wcel cn0 clt wbr csqr cfv wi cded wceq breq2 fveq2 breq2d bi2imd
    ax0re elimel sqrgt0 dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZ
    IADAUEKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( The square root of a non-negative real is non-negative. $)
  sqrge0t $p |- ( ( A e. RR /\ 0 _10 <_ A ) -> 0 _10 <_ ( sqr ` A ) ) $=
    ( cr wcel cn0 cle wbr csqr cfv wi cded wceq breq2 fveq2 breq2d bi2imd
    ax0re elimel sqrge0 dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZ
    IADAUEKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( A square root is zero iff its argument is 0. $)
  sqr00t $p |- ( ( A e. RR /\ 0 _10 <_ A ) ->
               ( ( sqr ` A ) = 0 _10 <-> A = 0 _10 ) ) $=
    ( cr wcel cn0 cle wbr wa csqr cfv wceq wb wi cded breq2 birand fveq2
    cleq1d cleq1 bi2bid bi2imd ax0re elimel sqr11 dedth cleqid eqle ax-mp
    mpan2i imp sqr0 cleq2i syl5bbr ) ABCZDAEFZGAHIZDHIZJZADJZUODJUMUNUQURKZUMUN
    DDEFZUSUMUNUTGZUSLDUMADMZEFZUTGZVBHIZUPJZVBDJZKZLADAVBJZVAVDUSVHVIUNVCUTAVB
    DENOVIUQVFURVGVIUOVEUPAVBHPQAVBDRSTVBDADBUAUBUAUCUDDDJUTDUEDDUAUAUFUGUHUIUP
    DUOUJUKUL $.
    $([27-Jul-99]$)


