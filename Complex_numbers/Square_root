$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Square root
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c sqr $. $( Positive square root of a positive real number. $)

  $( Extend class notation to include positive square root of a positive real
     number. $)
  csqr $a class sqr $.

  ${
    $d x y z $.
    $( Define a function whose value is the square root of a non-negative
       real number.  The square root of ` x ` is the supremum of all reals
       whose square is less than ` x `.  See ~ sqrcl for its closure,
       ~ sqrval for its value, ~ sqrsqe and ~ sqsqr for its relationship to
       squares, and ~ sqr11 for uniqueness. $)
    df-sqr $a |- sqr = { <. x , y >. | ( ( x e. RR /\ 0_10 <_ x ) /\
     y = sup ( { z e. RR | ( 0_10 <_ z /\ ( z x. z ) <_ x ) } , RR , < ) ) } $.
  $}

  ${
    $d x y z w A $.  $( ` y z w ` are dummy. $)
    $( Value of square root function. $)
    sqrval $p |- ( ( A e. RR /\ 0_10 <_ A ) -> ( sqr ` A ) =
          sup ( { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } , RR , < ) ) $=
      ( vw vy vz cr wcel cn0 cle wbr wa cv crab csqr cfv cmulc co clt csup
      wceq breq2 rabcl biland rbiabdv supeq1 syl copab df-sqr biran biopab
      eqtr4 ltso supex opabval2 sylbir ) BFGHBIJZKBHCLZIJZCFMZGBNOHALZIJZUTUTPQ
      ZBIJZKZAFMZFRSZTURUPCBFUQBHIUAUBDEBVAVBDLZIJZKZAFMZFRSZVFUSNVGBTZVJVETVKV
      FTVLVIVDAFVLVHVCVAVGBVBIUAUCUDFVJVERUEUFNVGFGHVGIJZKZELVKTZKZDEUGVGUSGZVO
      KZDEUGDEAUHVRVPDEVQVNVOURVMCVGFUQVGHIUAUBUIUJUKFVERULUMUNUO $.
      $([23-May-99]$)
  $}

  $( Square root of zero. $)
  sqr0 $p |- ( sqr ` 0_10 ) = 0_10 $=
    ( vx cn0 csqr cfv csn cr clt csup cv cle wbr cmulc co wa crab wcel wceq
    ax0re cleqid eqle ax-mp sqrval mp2an cab wn wb axmulrcl anabss jctir leltt
    syl ltsqt pm2.15d sylbid adantld imp 0cn mulzer1 mulrcl pm3.2i eleq1 breq2
    opreq12 breq1d bi2and mpbiri impbi biab df-rab df-sn 3eqtr4 supeq1 eqtr
    ltso supsn ) BCDZBEZFGHZBVPBAIZJKZVSVSLMZBJKZNZAFOZFGHZVRBFPZBBJKZVPWEQRBBQ
    WGBSBBRRTUAZABUBUCWDVQQWEVRQVSFPZWCNZAUDVSBQZAUDWDVQWJWKAWJWKWIWCWKWIWBWKVT
    WIWBBWAGKZUEZWKWIWAFPZWFNWBWMUFWIWNWFWIWNVSVSUGUHRUIWABUJUKWIWKWLVSULUMUNUO
    UPWKWJWFWGBBLMZBJKZNZNWFWQRWGWPWHWOBQWPBUQURWOBBBRRUSRTUAUTUTWKWIWFWCWQVSBF
    VAWKVTWGWBWPVSBBJVBWKWAWOBJWKWAWOQVSBVSBLVCUHVDVEVEVFVGVHWCAFVIABVJVKFWDVQG
    VLUAVMWFVRBQRFBGVNVOUAVM $.
    $([23-May-99]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    sqrlem1.1 $e |- A e. RR $.
    sqrlem1.2 $e |- 0_10 < A $.

    $( Lemma for square root theorem. $)
    sqrlem1 $p |- A < ( ( 1_10 + A ) x. ( 1_10 + A ) ) $=
      ( cn1 caddc co clt wbr cmulc cn0 recn addid2 lt01 ax0re ax1re ltadd1
      mpbi eqbrtrr addrcl mulid2 1cn addid1 ltadd2 addgt0i ltmul1i mulrcl lttr
      mp2an ) ADAEFZGHUIUIUIIFZGHAUJGHJAEFZAUIGAABKLJDGHUKUIGHMJDANOBPQRDUIIFZU
      IUJGUIUIDAOBSZKTDUIGHULUJGHDJEFZDUIGDUAUBJAGHUNUIGHCJADNBOUCQRDUIUIOUMUMD
      AOBMCUDUEQRAUIUJBUMUIUIUMUMUFUGUH $.
      $([16-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem2 $p |- ( ( A / ( 1_10 + A ) ) x. ( A / ( 1_10 + A ) ) ) < A $=
      ( cn1 caddc co cdiv cmulc clt wbr sqrlem1 cn0 wb ax1re addrcl mulrcl
      ltmul2 ax-mp mpbi ax0re lttr mp2an ltdivi recn lt01 addgt0i gt0ne0i
      divmuldiv divass divid opreq2i eqtr mulid1 eqtr2 breq12i mpbir ) ADAEFZGF
      ZURHFZAIJAAHFZUQUQHFZGFZAVAHFZVAGFZIJZUTVCIJZVEAVAIJZVFABCKZLAIJZVGVFMCAV
      AABUQUQDANBOZVJPZBQRSUTVCVAAABBPAVABVKPVKVIVGLVAIJCVHLAVATBVKUAUBZUCSUSVB
      AVDIAUQAUQABUDZUQVJUDZVMVNUQVJDANBUECUFUGZVOUHVDADHFZAVDAVAVAGFZHFVPAVAVA
      VMVAVKUDZVRVAVKVLUGZUIVQDAHVAVRVSUJUKULAVMUMUNUOUP $.
      $([16-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem3 $p |- 0_10 < ( A / ( 1_10 + A ) ) $=
      ( cn1 caddc co ax1re addrcl lt01 addgt0i divgt0i ) ADAEFBDAGBHCDAGBICJK
      $.
      $([16-May-99]$)

    ${
      $d x y A $.  $d x y S $.  $d x D $.
      sqrlem4.3 $e |- S = { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } $.
      $( Lemma for square root theorem. $)
      sqrlem4 $p |- ( D e. S <->
                    ( D e. RR /\ ( 0_10 <_ D /\ ( D x. D ) <_ A ) ) ) $=
        ( wcel cn0 cv cle wbr cmulc co wa cr crab eleq2i wceq breq2 opreq12
        anabss breq1d bi2and rabcl bitr ) CDHCIAJZKLZUGUGMNZBKLZOZAPQZHCPHICKLZ
        CCMNZBKLZOZODULCGRUKUPACPUGCSZUHUMUJUOUGCIKTUQUIUNBKUQUIUNSUGCUGCMUAUBU
        CUDUEUF $.
        $([21-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem5 $p |- ( ( D e. RR /\ ( 0_10 < D /\ ( D x. D ) < A ) ) ->
                        D e. S ) $=
        ( cr wcel cn0 clt wbr cmulc co wa cle wi ax0re wceq wo leloet orc
        syl5bir mpan axmulrcl anabss sylan mpan2 im2and imdistani sqrlem4
        sylibr ) CHIZJCKLZCCMNZBKLZOZOUMJCPLZUOBPLZOZOCDIUMUQUTUMUNURUPUSJHIZUM
        UNURQRVAUMOURUNJCSZTUNJCUAUNVBUBUCUDUMBHIZUPUSQZEUOHIZVCVDUMVEVCOUSUPUO
        BSZTUPUOBUAUPVFUBUCUMVECCUEUFUGUHUIUJABCDEFGUKUL $.
        $([23-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem6 $p |- ( S (_ RR /\ S =/= (/) /\
                     E. x e. RR A. y e. S y < x ) $=
        ( cr wss c0 wne cv clt wbr wral wrex cn0 cle cmulc co wa crab ssrab
        eqsstr wceq wn wcel ax0re cleqid wo olc leloe sylibr ax-mp 0cn mulzer1
        ltlei eqbrtr pm3.2i sqrlem4 mpbir noel2 df-ne cn1 caddc ax1re addrcl
        wi axmulrcl anabss leloet mulrcl axlttrn mp3an3 sqrlem1 jctr syl5
        breq1 mpbiri a1i jaod sylbid mpan2 syl wb mpan cded breq2 opreq12
        breq1d bi2imd lt01 addgt0i elimel lt2sq ltle syl2an biimprd dedth
        mpbii a1d com23 syld imp32 sylbi rgen biraldv rcla4ev mp2an 3pm3.2i )
        DHIDJKZBLZALZMNZBDOZAHPZDQXMRNXMXMSTCRNUAZAHUBHGXQAHUCUDXKDJUEUFZQDUGZX
        RXSQHUGZQQRNZQQSTZCRNZUAZUAXTYDUHYAYCQQUEZYAQUIYEQQMNZYEUJYAYEYFUKQQUHU
        HULUMUNYBQCRQUOUPQCUHEFUQURUSUSACQDEFGUTVADQVBUNDJVCVAVDCVETZHUGXLYGMNZ
        BDOZXPVDCVFEVGZYHBDXLDUGXLHUGZQXLRNZXLXLSTZCRNZUAUAYHACXLDEFGUTYKYLYNYH
        YKYNYLYHYKYNYMYGYGSTZMNZYLYHVHYKYMHUGZYNYPVHZYKYQXLXLVIVJYQCHUGZYREYQYS
        UAZYNYMCMNZYMCUEZUJYPYMCVKYTUVAYPUVBYTUVACYOMNZUAZYPUVAYQYSYOHUGUVDYPVH
        YGYGYJYJVLYMCYOVMVNUVAUVCCEFVOZVPVQUVBYPVHYTUVBYPUVCUVEYMCYOMVRVSVTWAWB
        WCWDYKYLYPYHYKYLQXLMNZQXLUEZUJZYPYHVHZXTYKYLUVHWEUHQXLVKWFYKUVFUVIUVGYK
        UVFUVIVHQYKXLVDWGZMNZUVJUVJSTZYOMNZUVJYGMNZVHZVHXLVDXLUVJUEZUVFUVKUVIUV
        OXLUVJQMWHUVPYPUVMYHUVNUVPYMUVLYOMUVPYMUVLUEXLUVJXLUVJSWIVJWJXLUVJYGMVR
        WKWKUVKUVNUVMUVKQYGMNZUVNUVMWEZVDCVFEWLFWMZQUVJRNQYGRNUVRUVKUVQUVJYGXLV
        DHVFWNZYJWOQUVJUHUVTWPQYGUHYJWPWQWCWRWSUVGUVIVHYKUVGYHYPUVGUVQYHUVSQXLY
        GMVRWTXAVTWAWBXBXCXBXDXEXFXOYIAYGHXMYGUEXNYHBDXMYGXLMWHXGXHXIXJ $.
        $([21-May-99]$)

      ${
        $d x y z S $.  $d z A $.
        sqrlem7.4 $e |- B = sup ( S , RR , < ) $.
        $( Lemma for square root theorem. $)
        sqrlem7 $p |- B e. RR $=
          ( vy vz cr wcel clt csup ltso wss c0 wne cv wbr wral wrex w3a wn wi
          wa sqrlem6 axsup ax-mp supcl eleq1i mpbir ) CKLDKMNZKLAIJKDMODKPDQRIS
          ZASZMTZIDUAAKUBUCUOUNMTUDIDUAUPUNJSMTJDUBUEIKUAUFAKUBAIBDEFGUGAIJDUHU
          IUJCUMKHUKUL $.
          $([24-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem8 $p |- 0_10 < B $=
          ( vy vz cn0 cr clt csup cn1 caddc co cdiv wbr cle sqrlem3 wn wcel
          cmulc wa ax1re addrcl lt01 addgt0i gt0ne0i divrcl sqrlem2 pm3.2i
          sqrlem5 mp2an ltso wss c0 wne cv wral wrex w3a wi sqrlem6 axsup
          ax-mp supub sqrlem7 eleq1i mpbi lelt mpbir ax0re ltletr breqtrr ) KDL
          MNZCMKBOBPQZRQZMSZVSVQTSZKVQMSBEFUAZWAVQVSMSUBZVSDUCZWCVSLUCVTVSVSUDQ
          BMSZUEWDBVREOBUFEUGZVRWFOBUFEUHFUIUJUKZVTWEWBBEFULUMABVSDEFGUNUOAIJLD
          VSMUPDLUQDURUSIUTZAUTZMSZIDVAALVBVCWIWHMSUBIDVAWJWHJUTMSJDVBVDILVAUEA
          LVBAIBDEFGVEAIJDVFVGVHVGVSVQWGCLUCVQLUCABCDEFGHVICVQLHVJVKZVLVMKVSVQV
          NWGWKVOUOHVP $.
          $([24-May-99]$)

      $}
    $}

    ${
      sqrlem9.3 $e |- B e. RR $.
      sqrlem9.4 $e |- C e. RR $.
      sqrlem9.5 $e |- 0_10 < B $.
      sqrlem9.6 $e |- A < ( B x. B ) $.
      sqrlem9.7 $e |- C = ( ( B + ( A / B ) ) / ( 1_10 + 1_10 ) ) $.
      $( Lemma for square root theorem. $)
      sqrlem9 $p |- 0_10 < C $=
        ( cn0 cdiv co caddc cn1 clt gt0ne0i divrcl addrcl ax1re divgt0i addgt0i
        lt01 breqtrr ) KBABLMZNMZOONMZLMCPUFUGBUEFABDFBFHQRZSOOTTSBUEFUHHABDFEH
        UAUBOOTTUCUCUBUAJUD $.
        $([17-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem10 $p |- C < B $=
        ( cdiv co caddc cn1 clt cmulc wbr mulrcl ltdivi mpbi recn gt0ne0i divass
        divid opreq2i mulid1 3eqtr breqtr divrcl ltadd2 1cn addcl mulcom
        1p1times eqtr breqtrr addrcl ax1re lt01 addgt0i eqbrtr ) CBABKLZMLZNNML
        ZKLZBOJVEBVDPLZVDKLZBOVCVFOQVEVGOQVCBBMLZVFOVBBOQVCVHOQVBBBPLZBKLZBOAVI
        OQVBVJOQIAVIBDBBFFRFHSTVJBBBKLZPLBNPLZBBBBBFUAZVMVMBFHUBZUCVKNBPBVMVNUD
        UEBVMUFZUGUHVBBBABDFVNUIZFFUJTVFVDBPLVHBVDVMNNUKUKULZUMBVMUNUOUPVCVFVDB
        VBFVPUQBVDFNNURURUQZRVRNNURURUSUSUTZSTVGBVDVDKLZPLVLBBVDVDVMVQVQVDVRVSU
        BZUCVTNBPVDVQWAUDUEVOUGUHVA $.
        $([17-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem11 $p |- A < ( C x. C ) $=
        ( cn1 caddc co cmulc cdiv clt wbr cneg cn0 cmin mulrcl recn 1cn addcl
        negcl negrcl mul12 mulneg1 1p1times negeqi df-neg eqtr 3eqtr wceq wn
        wne addrcl negid ltadd1 mpbi eqbrtrr gt0ne0i df-ne sqgt0 ax-mp muladd
        breqtr ax0re ltsubadd mpbir eqbrtr ax1re mulgt0i ltdivi mulcl muln0
        divcan3 mul2neg divdistr breq12i divrcl mulcom mulass divcl divcan4
        cleqcomi divmuldiv opreq1i eqtr4 opreq12i divcan2 opreq2i eqtr3 lt01
        addgt0i eqtr2 ) AKKLMZWQNMZNMZWROMZBABOMZLMZXBNMZWROMZPQZACCNMZPQWSXCPQ
        ZXEXGWQANMZXHLMZBBNMZXJNMZXJOMZARZXMNMZXJOMZLMZXHLMZPQZXHXPPQZXRXJWQRZX
        MNMZNMZXJOMZXKXNLMZXJOMZPQZXSYBYDPQYFYBSXJXMNMZYGLMZTMZYDPYBXTYGNMWQYGN
        MZRZYIXJXTXMXJBBFFUAZUBZWQKKUCUCUDZUEZXMADUFZUBZUGWQYGYNYGXJXMYLYPUAZUB
        ZUHYKYHRYIYJYHYGYSUIUJYHUKULUMYIYDPQSYDYHLMZPQSXJXMLMZUVANMZYTPUVASUNUO
        ZSUVBPQUVASUPUVCUVAXJXMYLYPUQZAXMLMZSUVAPAADUBZURAXJPQUVEUVAPQIAXJXMDYL
        YPUSUTVAVBUVASVCUTUVAUVDVDVEXJXMXJXMYMYQYMYQVFVGSYHYDVHYGYGYRYRUQXKXNXJ
        XJYLYLUAZXMXMYPYPUAZUQZVIVJVKYBYDXJXJYAYLXTXMWQKKVLVLUQZUFYPUAUAUVIYLBB
        FFHHVMZVNUTYCXHYEXPPYCYAXHXJYAYMXTXMYOYQVOBBBFUBZUVLBFHVBZUVMVPVQWQAYNU
        VFVRULXKXNXJXKUVGUBXNUVHUBYMXJYLUVKVBZVSVTUTXHXPXHWQAUVJDUAZXLXOXKXJUVG
        YLUVNWAXNXJUVHYLUVNWAUQUVOUSUTWSXIXCXQPWSWRANMWQXHNMXIAWRUVFWQWQYNYNVOW
        BWQWQAYNYNUVFWCXHWQAYNUVFVOUIUMXCXJXAXANMZLMZBXANMZUVRLMZLMXQBXABXAUVLA
        BUVFUVLUVMWDZUVLUVTVFUVQXPUVSXHLXJXLUVPXOLXLXJXJXJYMYMUVNWEWFUVPAANMZXJ
        OMXOABABUVFUVLUVFUVLUVMUVMWGXNUWAXJOAAUVFUVFVRWHWIWJWQUVRNMUVSXHUVRBXAU
        VLUVTVOUIUVRAWQNBAUVLUVFUVMWKWLWMWJULVTVJWSXCWRAWRDWQWQUVJUVJUAZUAXBXBB
        XAFABDFUVMWAUQZUWCUAUWBWQWQUVJUVJKKVLVLWNWNWOZUWDVMZVNUTWTAXDXFPWRAWRUW
        BUBUVFWRUWBUWEVBWEXFXBWQOMZUWFNMXDCUWFCUWFNJJWJXBWQXBWQBXAUVLUVTUDZYNUW
        GYNWQUVJUWDVBZUWHWGWPVTUT $.
        $([18-May-99]$)

      ${
        $d x y A $.  $d x y z S $.  $d y z C $.  $d x D $.
        sqrlem12.8 $e |- S = { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } $.

        $( Lemma for square root theorem. $)
        sqrlem12 $p |- ( D e. S -> D < C ) $=
          ( wcel cn0 clt wbr wceq wo cle cmulc co cr wa sqrlem4 pm3.27bd
          pm3.26d wb pm3.26bd ax0re leloet mpan syl mpbid pm3.27d sqrlem11 wi
          axmulrcl anabss mulrcl lelttrt mp3an3 mpan2 mpan2i mpd adantr
          sqrlem9 cded breq2 birand breq1 opreq12 breq1d bi2bid bi2imd elimel
          lt2sq ltle syl2an dedth imp mpbird exp mpbii a1i jaod ) EFOZPEQRZPESZ
          TZEDQRZWHPEUARZWKWHWMEEUBUCZBUARZWHEUDOZWMWOUEZABEFGHNUFZUGZUHWHWPWMW
          KUIZWHWPWQWRUJZPUDOWPWTUKPEULUMUNUOWHWIWLWJWHWIWLWHWIUEWLWNDDUBUCZQRZ
          WHXCWIWHWOXCWHWMWOWSUPWHWOBXBQRZXCBCDGHIJKLMUQWHWNUDOZWOXDUEXCURZWHWP
          XEXAWPXEEEUSUTUNXEBUDOZXFGXEXGXBUDOXFDDJJVAWNBXBVBVCVDUNVEVFVGWHWIWLX
          CUIZWHWIPDQRZXHBCDGHIJKLMVHZWHWPWIXIUEZXHURZXAWPXLPWPEPVIZQRZXIUEZXMD
          QRZXMXMUBUCZXBQRZUIZUREPEXMSZXKXOXHXSXTWIXNXIEXMPQVJVKXTWLXPXCXREXMDQ
          VLXTWNXQXBQXTWNXQSEXMEXMUBVMUTVNVOVPPXMUARPDUARXSXNXIXMDEPUDUKVQZJVRP
          XMUKYAVSPDUKJVSVTWAUNVEWBWCWDWJWLURWHWJXIWLXJPEDQVLWEWFWGVF $.
          $([23-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem13 $p |- ( B = sup ( S , RR , < ) -> -. C < B ) $=
          ( vz vy cr clt csup wceq wbr wn wcel cv wral wo wa sqrlem12 wb cn0
          cle cmulc co sqrlem4 pm3.26bd axlttri mpan2 syl mpbid ioran sylib
          pm3.27d rgen ltso wss c0 wne wrex w3a wi sqrlem6 axsup ax-mp supnub
          mp2an breq2 binegd mpbiri ) CEPQRZSZDCQTZUADVRQTZUAZDPUBZDNUCZQTZUAZN
          EUDWBIWFNEWDEUBZWDDSZUAZWFWGWHWEUEUAZWIWFUFWGWDDQTZWJABCDWDEFGHIJKLMU
          GWGWDPUBZWKWJUHZWGWLUIWDUJTWDWDUKULBUJTUFABWDEFGMUMUNWLWCWMIWDDUOUPUQ
          URWHWEUSUTVAVBAONPEDQVCEPVDEVEVFOUCZAUCZQTZOEUDAPVGVHWOWNQTUAOEUDWPWN
          WDQTNEVGVIOPUDUFAPVGAOBEFGMVJAONEVKVLVMVNVSVTWACVRDQVOVPVQ $.
          $([25-May-99]$)

        $( Lemma for square root theorem. $)
        sqrlem14 $p |- -. B = sup ( S , RR , < ) $=
          ( cr clt csup wceq wbr sqrlem10 sqrlem13 mt2 ) CENOPQDCORBCDFGHIJKLSA
          BCDEFGHIJKLMTUA $.
          $([25-May-99]$)
      $}

    $}

    ${
      sqrlem15.3 $e |- B e. RR $.
      sqrlem15.4 $e |- 0_10 < B $.

      ${
        sqrlem15.5 $e |- C e. RR $.
        sqrlem15.6 $e |- 0_10 < C $.
        $( Lemma for square root theorem. $)
        sqrlem15 $p |- B < ( B + C ) $=
          ( cn0 caddc co clt recn addid1 wbr ax0re ltadd2 mpbi eqbrtrr ) BJKLZB
          BCKLZMBBFNOJCMPUAUBMPIJCBQHFRST $.
          $([24-May-99]$)

        ${
          sqrlem16.7 $e |- C < B $.
          $( Lemma for square root theorem. $)
          sqrlem16 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1_10 + 1_10 ) + 1_10 ) x. B ) ) ->
                          ( ( B + C ) x. ( B + C ) ) < A ) $=
            ( cmulc co cmin cn1 caddc cdiv clt wbr 1cn addcl ax1re recn mulass
            mulcl mulrcl subrcl addrcl lt01 addgt0i gt0ne0i muln0 divcan2
            breq12i cn0 wb mulgt0i divrcl ltmul2 ax-mp ltaddsub 3bitr4 mpbi
            ltadd2 ltadd1 muladd addcom distr2 1p1times mulid2 mulcom eqtr
            opreq12i 3eqtr opreq1i addass 3eqtr4 mpbir lttr mpan sylbi ) CABBKL
            ZMLZNNOLZNOLZBKLZPLZQRZWDBCKLZKLZWAOLZAQRZBCOLZWLKLZAQRZWECKLZWEWFK
            LZQRZWIWBQRWGWKWOWIWPWBQWDBCWCNNNSSTZNUAUBTZBFUBZCHUBZUCWEWBWDBWSWT
            UDWBAWADBBFFUEZUFZUBWDBWDWCNNNUAUAUGZUAUGZUBWTWDXEWCNXDUANNUAUAUHUH
            UIUHUIZUJBFGUJUKZULUMUNWEQRWGWQUOWDBXEFXFGUPCWFWEHWBWEXCWDBXEFUEZXG
            UQXHURUSWIWAAWDWHXEBCFHUEZUEZXBDUTVAWMWJQRZWKWNXKWACCKLZOLZWHWHOLZO
            LZWACBKLZOLZXNOLZQRZXMXQQRZXSXLXPQRZXTCBQRZYAJUNCQRYBYAUOICBCHFHURU
            SVBXLXPWACCHHUEZCBHFUEZXBVCVBXMXQXNWAXLXBYCUGWAXPXBYDUGWHWHXIXIUGZV
            DVBWMXOWJXRQBCBCWTXAWTXAVEXPXNOLZWAOLWAYFOLWJXRYFWAYFXPXNYDYEUGUBWA
            XBUBZVFWIYFWAOWIWCWHKLZNWHKLZOLXNXPOLYFWCNWHWRSWHXIUBZVGYHXNYIXPOWH
            YJVHYIWHXPWHYJVIBCWTXAVJVKVLXNXPXNYEUBZXPYDUBZVFVMVNWAXPXNYGYLYKVOV
            PUMVQWMWJAWLWLBCFHUGZYMUEWIWAXJXBUGDVRVSVT $.
            $([26-May-99]$)

          ${
            $d x y A $.  $d x y z B $.  $d x C $.  $d x y z S $.
            sqrlem17.8 $e |- S =
                             { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } $.
            $( Lemma for square root theorem. $)
            sqrlem17 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1_10 + 1_10 ) + 1_10 ) x. B ) ) ->
                          ( B + C ) e. S ) $=
              ( cmulc co cmin cn1 caddc cdiv clt wbr cn0 wa wcel sqrlem16
              addgt0i jctil cr addrcl sqrlem5 mpan syl ) DBCCNOPOQQROQROCNOSOTU
              AZUBCDROZTUAZUNUNNOBTUAZUCZUNEUDZUMUPUOBCDFGHIJKLUECDHJIKUFUGUNUH
              UDUQURCDHJUIABUNEFGMUJUKUL $.
              $([26-May-99]$)

            $( Lemma for square root theorem. $)
            sqrlem18 $p |- (  C < ( ( A - ( B x. B ) ) /
                          ( ( ( 1_10 + 1_10 ) + 1_10 ) x. B ) ) ->
                          -. B = sup ( S , RR , < ) ) $=
              ( vy vz cmulc co cmin cn1 caddc cdiv clt wbr cr csup wceq
              sqrlem15 wn breq1 binegd wcel sqrlem17 ltso wss c0 wne cv wral
              wrex w3a wi wa sqrlem6 axsup ax-mp supub syl syl5bir comm mt2i )
              DBCCPQRQSSTQSTQCPQUAQUBUCZCEUDUBUEZUFZCCDTQZUBUCZBCDFGHIJKUGVMVKV
              OUHZVMVPVLVNUBUCZUHZVKVMVOVQCVLVNUBUIUJVKVNEUKVRABCDEFGHIJKLMULAN
              OUDEVNUBUMEUDUNEUOUPNUQZAUQZUBUCZNEURAUDUSUTVTVSUBUCUHNEURWAVSOUQ
              UBUCOEUSVANUDURVBAUDUSANBEFGMVCANOEVDVEVFVGVHVIVJ $.
              $([26-May-99]$)

          $}
        $}
      $}

      ${
        sqrlem19.5 $e |- ( B x. B ) < A $.
        $( Lemma for square root theorem. $)
        sqrlem19 $p |- 0_10 < ( ( A - ( B x. B ) ) /
                      ( ( ( 1_10 + 1_10 ) + 1_10 ) x. B ) ) $=
          ( cmulc co cmin cn1 caddc mulrcl subrcl ax1re addrcl cn0 clt wbr
          recn addid2 eqbrtr ax0re ltaddsub mpbi lt01 addgt0i mulgt0i divgt0i
          ) ABBHIZJIZKKLIZKLIZBHIAUJCBBEEMZNUMBULKKKOOPZOPZEMQUJLIZARSQUKRSUQUJ
          ARUJUJUNTUAGUBQUJAUCUNCUDUEUMBUPEULKUOOKKOOUFUFUGUFUGFUHUI $.
          $([19-May-99]$)

        ${
          $d x y w A $.  $d x y z w B $.  $d x y z w S $.
          sqrlem20.6 $e |- S =
                             { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } $.
          $( Lemma for square root theorem. $)
          sqrlem20 $p |- -. B = sup ( S , RR , < ) $=
            ( vw cn0 cv clt wbr cmulc co cmin cn1 caddc cdiv wa cr wrex csup
            wceq wn mulrcl subrcl ax1re addrcl recn lt01 addgt0i gt0ne0i muln0
            divrcl sqrlem19 posex wcel wi w3a cded breq1 birimd eleq1 breq2
            bi3and divgt0i halfpos mpbi 3pm3.2i elimhyp 3simp1 ax-mp 3simp2
            3simp3 sqrlem18 dedth 3exp imp4d r19.23aiv ) LKMZNOZWCCNOZWCBCCPQZR
            QZSSTQZSTQZCPQZUAQZNOZUBUBZKUCUDCDUCNUEUFUGZKCWKGWGWJBWFECCGGUHUIWI
            CWHSSSUJUJUKZUJUKZGUHWICWIWPULCGULWIWPWHSWOUJSSUJUJUMUMUNZUMUNUOCGH
            UOUPUQHBCEFGHIURUSWMWNKUCWCUCUTZWDWEWLWNWRWDWEWLWNVAZWRWDWEVBZWSWTW
            CCWHUAQZVCZWKNOZWNVAWCXAWCXBUFZWLXCWNWCXBWKNVDVEABCXBDEFGHXBUCUTZLX
            BNOZXBCNOZVBZXEWTXHXAUCUTZLXANOZXACNOZVBWCXAXDWRXEWDXFWEXGWCXBUCVFW
            CXBLNVGWCXBCNVDVHXAXBUFXIXEXJXFXKXGXAXBUCVFXAXBLNVGXAXBCNVDVHXIXJXK
            CWHGWOWHWOWQUOUQCWHGWOHWQVILCNOXKHCGVJVKVLVMZXEXFXGVNVOXHXFXLXEXFXG
            VPVOXHXGXLXEXFXGVQVOJVRVSVTWAWBVO $.
            $([26-May-99]$)
        $}
      $}
    $}

    ${
      $d x y z A $.  $d x B $.  $d x S $.
      sqrlem21.3 $e |- S = { x e. RR | ( 0_10 <_ x /\ ( x x. x ) <_ A ) } $.
      sqrlem21.4 $e |- B = sup ( S , RR , < ) $.
      $( Lemma for square root theorem. $)
      sqrlem21 $p |- -. A < ( B x. B ) $=
        ( cmulc co clt wbr cr csup wceq wn cn1 caddc cded cleq1 binegd cdiv
        wcel eleq1 sqrlem7 ax1re addrcl keephyp id opreq2 opreq12d opreq1d
        eleq1d sqrlem8 gt0ne0i divrcl lt01 addgt0i cn0 breq2 opreq12 anabss
        breq2d sqrlem1 elimhyp cleqid sqrlem14 dedth mt2 ) BCCIJZKLZCDMKNZOZHVK
        VMPVKCQBRJZSZVLOZPCVNCVOOZVMVPCVOVLTUAABVOVOBVOUBJZRJZQQRJZUBJZDEFVKCMU
        CVNMUCVOMUCCVNCVOMUDVNVOMUDABCDEFGHUEZQBUFEUGZUHVKCBCUBJZRJZVTUBJZMUCVN
        BVNUBJZRJZVTUBJZMUCWAMUCCVNVQWFWAMVQWEVSVTUBVQCVOWDVRRVQUICVOBUBUJUKULU
        MVNVOOZWIWAMWJWHVSVTUBWJVNVOWGVRRWJUIVNVOBUBUJUKULUMWEVTCWDWBBCEWBCWBAB
        CDEFGHUNZUOUPUGQQUFUFUGZVTWLQQUFUFUQUQURUOZUPWHVTVNWGWCBVNEWCVNWCQBUFEU
        QFURZUOUPUGWLWMUPUHVKUSCKLUSVNKLUSVOKLCVNCVOUSKUTVNVOUSKUTWKWNUHVKBVOVO
        IJZKLBVNVNIJZKLCVNVQVJWOBKVQVJWOOCVOCVOIVAVBVCWJWPWOBKWJWPWOOVNVOVNVOIV
        AVBVCBEFVDVEWAVFGVGVHVI $.
        $([24-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem22 $p |- -. ( B x. B ) < A $=
        ( cmulc co clt wbr cr csup wceq wn cn1 caddc cdiv cded cleq1 binegd
        wcel eleq1 sqrlem7 ax1re addrcl lt01 addgt0i gt0ne0i divrcl keephyp cn0
        breq2 sqrlem8 sqrlem3 opreq12 anabss breq1d sqrlem2 elimhyp sqrlem20
        dedth mt2 ) CCIJZBKLZCDMKNZOZHVFVHPVFCBQBRJZSJZTZVGOZPCVJCVKOZVHVLCVKVG
        UAUBABVKDEFVFCMUCVJMUCVKMUCCVJCVKMUDVJVKMUDABCDEFGHUEBVIEQBUFEUGZVIVNQB
        UFEUHFUIUJUKULVFUMCKLUMVJKLUMVKKLCVJCVKUMKUNVJVKUMKUNABCDEFGHUOBEFUPULV
        FVKVKIJZBKLVJVJIJZBKLCVJVMVEVOBKVMVEVOOCVKCVKIUQURUSVJVKOZVPVOBKVQVPVOO
        VJVKVJVKIUQURUSBEFUTVAGVBVCVD $.
        $([26-May-99]$)

      $( Lemma for square root theorem. $)
      sqrlem23 $p |- ( B x. B ) = A $=
        ( cmulc co wceq clt wbr wn wa sqrlem22 sqrlem21 pm3.2i sqrlem7 mulrcl
        lttri3 mpbir ) CCIJZBKUCBLMNZBUCLMNZOUDUEABCDEFGHPABCDEFGHQRUCBCCABCDEF
        GHSZUFTEUAUB $.
        $([26-May-99]$)
    $}

    $( Lemma for square root closure. $)
    sqrlem24 $p |- ( sqr ` A ) e. RR $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup ax0re ltlei sqrval
      mp2an sqrlem7 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDOE
      DQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSBHAU
      HBCUIEAUJUKUL $.
      $([26-May-99]$)

    $( The square root of a positive real is positive. $)
    sqrgt0i $p |- 0_10 < ( sqr ` A ) $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup ax0re ltlei sqrval
      mp2an sqrlem8 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDOE
      DQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSBHAU
      HBCUIEAUJUKUL $.
      $([26-May-99]$)

    $( Lemma for square root theorem. $)
    sqrlem26 $p |- ( ( sqr ` A ) x. ( sqr ` A ) ) = A $=
      ( vy vx csqr cfv cn0 cv cle wbr cmulc co wa cr crab weq breq2 wceq
      opreq12 anabss breq1d bi2and cbvrabv wcel clt csup ax0re ltlei sqrval
      mp2an sqrlem23 ) DAAFGZHEIZJKZUNUNLMZAJKZNZEOPZBCURHDIZJKZUTUTLMZAJKZNEDO
      EDQZUOVAUQVCUNUTHJRVDUPVBAJVDUPVBSUNUTUNUTLTUAUBUCUDAOUEHAJKUMUSOUFUGSBHA
      UHBCUIEAUJUKUL $.
      $([24-May-99]$)

  $}

  ${
    sqrth.1 $e |- A e. RR $.
    $( Square root theorem.  Theorem I.35 of Apostol, p. 29. $)
    sqrth $p |- ( 0_10 <_ A -> ( ( sqr ` A ) x. ( sqr ` A ) ) = A ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv cmulc co ax0re leloe cn1 cded fveq2
      opreq12d id cleq12d cr ax1re keepel elimgt0 sqrlem26 dedth sqr0 opreq12i
      0cn mulzer1 eqtr mpbii jaoi sylbi ) CADECAFEZCAGZHAIJZUQKLZAGZCAMBNUOUSUP
      UOUSUOAOPZIJZVAKLZUTGAOAUTGZURVBAUTVCUQVAUQVAKAUTIQZVDRVCSTUTUOAOUABUBUCA
      UDUEUFUPCIJZVEKLZCGUSVFCCKLCVECVECKUGUGUHCUIUJUKUPVFURCAUPVEUQVEUQKCAIQZV
      GRUPSTULUMUN $.
      $([26-May-99]$)

    $( The square root of a non-negative real is a real. $)
    sqrcl $p |- ( 0_10 <_ A -> ( sqr ` A ) e. RR ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv cr wcel ax0re leloe cn1 cded fveq2
      eleq1d ax1re keepel elimgt0 sqrlem24 dedth sqr0 syl5eqr mpbii jaoi sylbi
      ) CADECAFEZCAGZHAIJZKLZCAMBNUIULUJUIULUIAOPZIJZKLAOAUMGUKUNKAUMIQRUMUIAOK
      BSTAUAUBUCUJCKLULMUJCUKKUJCIJUKCCAIQUDUERUFUGUH $.
      $([26-May-99]$)

    $( The square root of a positive real is positive. $)
    sqrgt0 $p |- ( 0_10 < A -> 0_10 < ( sqr ` A ) ) $=
      ( cn0 clt wbr csqr cfv cn1 cded wceq fveq2 breq2d cr ax1re keepel
      elimgt0 sqrgt0i dedth ) CADEZCAFGZDECSAHIZFGZDEAHAUAJTUBCDAUAFKLUASAHMBNO
      APQR $.
      $([26-May-99]$)

    $( The square root of a non-negative real is non-negative. $)
    sqrge0 $p |- ( 0_10 <_ A -> 0_10 <_ ( sqr ` A ) ) $=
      ( cn0 cle wbr clt wceq wo csqr cfv ax0re leloe cr wcel ltle sqrcl syl wb
      leloet mpan sqrgt0 orc syl5bir mpcom fveq2 sqr0 cleqcomi syl5eq cleqid
      eqle ax-mp syl5breq jaoi sylbi ) CADEZCAFEZCAGZHCAIJZDEZCAKBLUPUSUQURMNZU
      PUSUPUOUTCAKBOABPQUTUSCURFEZCURGZHZUPCMNUTUSVCRKCURSTUPVAVCABUAVAVBUBQUCU
      DUQCURCDUQCIJZURCCAIUEVDCUFUGUHCCGCCDECUICCKKUJUKULUMUN $.
      $([26-May-99]$)

    ${
      sqr11.1 $e |- B e. RR $.
      $( The square root function is one-to-one. $)
      sqr11 $p |- ( ( 0_10 <_ A /\ 0_10 <_ B ) ->
                 ( ( sqr ` A ) = ( sqr ` B ) <-> A = B ) ) $=
        ( cn0 cle wbr wa csqr cfv wceq cmulc co opreq12 anabss adantl sqrth
        ad2antll ad2antlr 3eqtr3d exp wi fveq2 a1i impbid ) EAFGZEBFGZHZAIJZBIJ
        ZKZABKZUHUKULUHUKHUIUILMZUJUJLMZABUKUMUNKZUHUKUOUIUJUIUJLNOPUFUMAKUGUKA
        CQRUGUNBKUFUKBDQSTUAULUKUBUHABIUCUDUE $.
        $([27-Jul-99]$)

      ${
        sqrmuli.1 $e |- 0_10 <_ A $.
        sqrmuli.2 $e |- 0_10 <_ B $.
        $( Square root distributes over multiplication. $)
        sqrmuli $p |- ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) $=
          ( cmulc co csqr cfv wceq cn0 cle wbr sqrth ax-mp opreq12i cr wcel
          sqrcl recn mul4 mulge0 mp2an mulrcl 3eqtr4r wb sqrge0 sq11 mpbi ) ABG
          HZIJZULGHZAIJZBIJZGHZUPGHZKZULUPKZUNUNGHZUOUOGHZGHUKUQUMUTAVABGLAMNZU
          TAKEACOPLBMNZVABKFBDOPQUNUOUNUOUNVBUNRSEACTPZUAZUOVCUORSFBDTPZUAZVEVG
          UBLUKMNZUMUKKVBVCVHEFABCDUCUDZUKABCDUEZOPUFLULMNZLUPMNZURUSUGVHVKVIUK
          VJUHPLUNMNZLUOMNZVLVBVMEACUHPVCVNFBDUHPUNUOVDVFUCUDULUPVHULRSVIUKVJTP
          UNUOVDVFUEUIUDUJ $.
          $([30-Jul-99]$)
      $}

      $( Square root distributes over multiplication. $)
      sqrmul $p |- ( ( 0_10 <_ A /\ 0_10 <_ B ) ->
                 ( sqr ` ( A x. B ) ) = ( ( sqr ` A ) x. ( sqr ` B ) ) ) $=
        ( cn0 cle wbr cmulc co csqr cfv wceq cded opreq1 fveq2d fveq2 opreq1d
        cleq12d opreq2 opreq2d cr ax0re keepel elimge0 sqrmuli dedth2h ) EAFGZE
        BFGZABHIZJKZAJKZBJKZHIZLUGAEMZBHIZJKZUNJKZULHIZLUNUHBEMZHIZJKZUQUSJKZHI
        ZLABEEAUNLZUJUPUMURVDUIUOJAUNBHNOVDUKUQULHAUNJPQRBUSLZUPVAURVCVEUOUTJBU
        SUNHSOVEULVBUQHBUSJPTRUNUSUGAEUACUBUCUHBEUADUBUCAUDBUDUEUF $.
        $([30-Jul-99]$)

      $( Relationship between square root and squares. $)
      sqrsq $p |- ( ( 0_10 <_ A /\ 0_10 <_ B ) ->
         ( ( sqr ` A ) = B <-> A = ( B x. B ) ) ) $=
        ( cn0 cle wbr wa csqr cfv cmulc co wceq wb sqrmul anabss sqrth eqtrd
        cleq2d adantl sqge0 mulrcl sqr11 mpan2 adantr bitr3d ) EAFGZEBFGZHAIJZB
        BKLZIJZMZUIBMZAUJMZUHULUMNUGUHUKBUIUHUKBIJZUOKLZBUHUKUPMBBDDOPBDQRSTUGU
        LUNNZUHUGEUJFGUQBDUAAUJCBBDDUBUCUDUEUF $.
        $([31-Jul-99]$)

      $( Square root is monotonic. $)
      sqrle $p |- ( ( 0_10 <_ A /\ 0_10 <_ B ) ->
          ( A <_ B <-> ( sqr ` A ) <_ ( sqr ` B ) ) ) $=
        ( cn0 cle wbr wa csqr cfv cmulc co wb cr wcel wi le2sqt sqrcl syl2an
        sqrge0 syl2and pm2.43i sqrth breqan12d bitrd bicomd ) EAFGZEBFGZHZAIJZB
        IJZFGZABFGZUIULUJUJKLZUKUKKLZFGZUMUIULUPMZUIEUJFGZEUKFGZUQUGUHUJNOUKNOU
        RUSHUQPUGUHUJUKQACRBDRSACTBDTUAUBUGUHUNAUOBFACUCBDUCUDUEUF $.
        $([3-Aug-99]$)

    $}

    $( Square root of square. $)
    sqrsqid $p |- ( 0_10 <_ A -> ( sqr ` ( A x. A ) ) = A ) $=
      ( cn0 cle wbr cmulc co csqr cfv wceq wa cleqid mulrcl sqrsq mpbiri sqge0
      a1i sylan anabss ) CADEZAAFGZHIAJZCUADEZTUBTUCTKUBUAUAJUALUAAAABBMBNOUCTA
      BPQRS $.
      $([2-Aug-99]$)
  $}

  $( The square root of a non-negative real is a real. $)
  sqrclt $p |- ( ( A e. RR /\ 0_10 <_ A ) -> ( sqr ` A ) e. RR ) $=
    ( cr wcel cn0 cle wbr csqr cfv wi cded wceq breq2 fveq2 eleq1d bi2imd
    ax0re elimel sqrcl dedth imp ) ABCZDAEFZAGHZBCZUAUBUDIDUAADJZEFZUEGHZBCZIAD
    AUEKZUBUFUDUHAUEDELUIUCUGBAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( The square root of a positive real is positive. $)
  sqrgt0t $p |- ( ( A e. RR /\ 0_10 < A ) -> 0_10 < ( sqr ` A ) ) $=
    ( cr wcel cn0 clt wbr csqr cfv wi cded wceq breq2 fveq2 breq2d bi2imd
    ax0re elimel sqrgt0 dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZ
    IADAUEKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( The square root of a non-negative real is non-negative. $)
  sqrge0t $p |- ( ( A e. RR /\ 0_10 <_ A ) -> 0_10 <_ ( sqr ` A ) ) $=
    ( cr wcel cn0 cle wbr csqr cfv wi cded wceq breq2 fveq2 breq2d bi2imd
    ax0re elimel sqrge0 dedth imp ) ABCZDAEFZDAGHZEFZUAUBUDIDUAADJZEFZDUEGHZEFZ
    IADAUEKZUBUFUDUHAUEDELUIUCUGDEAUEGMNOUEADBPQRST $.
    $([29-May-99]$)

  $( A square root is zero iff its argument is 0. $)
  sqr00t $p |- ( ( A e. RR /\ 0_10 <_ A ) ->
               ( ( sqr ` A ) = 0_10 <-> A = 0_10 ) ) $=
    ( cr wcel cn0 cle wbr wa csqr cfv wceq wb cleqid ax0re eqle ax-mp wi cded
    breq2 birand fveq2 cleq1d cleq1 bi2bid bi2imd elimel sqr11 dedth mpan2i
    imp sqr0 cleq2i syl5bbr ) ABCZDAEFZGAHIZDHIZJZADJZUODJUMUNUQURKZUMUNDDEFZUS
    DDJUTDLDDMMNOUMUNUTGZUSPDUMADQZEFZUTGZVBHIZUPJZVBDJZKZPADAVBJZVAVDUSVHVIUNV
    CUTAVBDERSVIUQVFURVGVIUOVEUPAVBHTUAAVBDUBUCUDVBDADBMUEMUFUGUHUIUPDUOUJUKUL
    $.
    $([27-Jul-99]$)


