$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define division, the (unique) element ` x ` such that ` B x = A `.
       This definition is meaningful only when ` B ` is not 0. $)
    df-div $a |- / = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. ( CC \ { 0 _10 } ) ) /\
                   z = U. { w e. CC | ( y x. w ) = x } ) } $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. CC /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}


  ${
    $d x y z w A $.  $d x y z w B $.  $( ` y z w ` are dummy. $)
    divval.1 $e |- A e. CC $.
    divval.2 $e |- B e. CC $.
    divval.3 $e |- B =/= 0 _10 $.
    $( Value of division: the (unique) element ` x ` such that ` B x = A `.
       This is meaningful only when ` B ` is not zero. $)
    divval $p |- ( A / B ) = U. { x e. CC | ( B x. x ) = A } $=
      ( vy vz vw cc wcel cn0 csn cdif cdiv co cv cmulc wceq crab cuni axcnex
      zfausrab zfun2 cleq2 rbiabdv unieqd opreq1 cleq1d df-div oprabval2 wn wa
      eldif elisseti elsnc bineg wne df-ne mpbi mpbir pm3.2i mp2an ) BJKCJLMZNZ
      KZBCOPCAQZRPZBSZAJTZUAZSGHIBCJVEHQZVGRPZGQZSZAJTZUAVKOVMBSZAJTZUAVJVIAJUB
      UCUDVNBSZVPVRVSVOVQAJVNBVMUEUFUGVLCSZVRVJVTVQVIAJVTVMVHBVLCVGRUHUIUFUGGHI
      AUJUKDVFCJKZCVDKZULZUMCJVDUNWAWCEWCCLSZULZWBWDCLCJEUOUPUQCLURWECLUSFUTVAV
      BVAVC $.
      $([8-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 _10 $.
    $( Relationship between reciprocal and multiplication. $)
    divmul   $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx cc wcel cdiv co wceq cmulc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu reuuni receux mpan2 cleqcom divval cleq2i bitr syl6rbbr
      vtoclga ax-mp ) CIJABKLZCMZBCNLZAMZOZFUNHPZMZBUSNLZAMZOURHCIUSCMZUTUOVBUQ
      USCUNQVCVAUPAUSCBNRSTUSIJZVBUSVBHIUAUBZMZUTVDVBHIUCVBVFOVBHIUDHBAEDGUEUFU
      TUSUNMVFUNUSUGUNVEUSHABDEGUHUIUJUKULUM $.
      $([2-Feb-95]$)
  $}

  ${
    divmult.1 $e |- A e. CC $.
    divmult.2 $e |- B e. CC $.
    divmult.3 $e |- C e. CC $.
    $( Relationship between reciprocal and multiplication. $)
    divmult   $p |- ( B =/= 0 _10 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cn0 wne cdiv co wceq cmulc wb cn1 cded opreq2 cleq1d opreq1 bi2bid cc
      1cn keepel elimne0 divmul dedth ) BGHZABIJZCKZBCLJZAKZMAUFBNOZIJZCKZUKCLJ
      ZAKZMBNBUKKZUHUMUJUOUPUGULCBUKAIPQUPUIUNABUKCLRQSAUKCDUFBNTEUAUBFBUCUDUE
      $.
      $([8-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 _10 $.
    $( Closure law for division. $)
    divcl   $p |- ( A / B ) e. CC $=
      ( vx cdiv co cv cmulc wceq cc crab cuni divval wreu wcel receux reucl
      ax-mp eqeltr ) ABGHBFIJHAKZFLMNZLFABCDEOUBFLPUCLQFBADCERUBFLSTUA $.
      $([2-Feb-95]$)
  $}

  ${
    divclt.1 $e |- A e. CC $.
    divclt.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclt   $p |- ( B =/= 0 _10 -> ( A / B ) e. CC ) $=
      ( cn0 wne cdiv co cc wcel cn1 cded wceq opreq2 eleq1d 1cn keepel elimne0
      divcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $([7-May-99]$)
  $}

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- A =/= 0 _10 $.
      $( A cancellation law for division. $)
      divcan2   $p |- ( A x. ( B / A ) ) = B $=
        ( cdiv co wceq cmulc divcl divmul cleqid mpbi ) BAFGZNHANIGBHBANDCBADCE
        JEKNLM $.
        $([9-Feb-95]$)

      $( A cancellation law for division. $)
      divcan1   $p |- ( ( B / A ) x. A ) = B $=
        ( cdiv co divcl divcan2 mulcoml ) ABAFGBCBADCEHDABCDEIJ $.
        $([18-May-99]$)
    $}

    divcan2t $p |- ( A =/= 0 _10 -> ( A x. ( B / A ) ) = B ) $=
      ( cn0 wne cdiv co cmulc wceq cn1 cded id opreq2 opreq12d cleq1d cc 1cn
      keepel elimne0 divcan2 dedth ) AEFZABAGHZIHZBJUCAKLZBUFGHZIHZBJAKAUFJZUEU
      HBUIAUFUDUGIUIMAUFBGNOPUFBUCAKQCRSDATUAUB $.
      $([10-Aug-99]$)
  $}

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 _10 $.
    divneq0.4 $e |- B =/= 0 _10 $.
    $( The ratio of non-zero numbers is non-zero. $)
    divneq0   $p |- ( A / B ) =/= 0 _10 $=
      ( cdiv co cn0 wne wceq wn cmulc 0cn divmul mulzer1 cleq1i cleqcom 3bitr
      bineg df-ne 3bitr4 mpbir ) ABGHZIJZAIJZUDIKZLAIKZLUEUFUGUHUGBIMHZAKIAKUHA
      BICDNFOUIIABDPQIARSTUDIUAAIUAUBEUC $.
      $([9-Feb-95]$)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a non-zero number is non-zero. $)
    recneq0z $p |- ( A =/= 0 _10 -> ( 1 _10 / A ) =/= 0 _10 ) $=
      ( cn0 wne cn1 cdiv co cded wceq wb opreq2 neeq1 syl 1cn cc keepel ax1ne0
      elimne0 divneq0 dedth ) ACDZEAFGZCDZEUAAEHZFGZCDZAEAUDIUBUEIUCUFJAUDEFKUB
      UECLMEUDNUAAEOBNPQARST $.
      $([14-May-99]$)
  $}

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 _10 $.
    $( Multiplication of a number and its reciprocal. $)
    recid   $p |- ( A x. ( 1 _10 / A ) ) = 1 _10 $=
      ( cn1 1cn divcan2 ) ADBECF $.
      $([9-Feb-95]$)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidz   $p |- ( A =/= 0 _10 -> ( A x. ( 1 _10 / A ) ) = 1 _10 ) $=
      ( cn0 wne cn1 cdiv co cmulc wceq cded id opreq2 opreq12d cleq1d cc 1cn
      keepel elimne0 recid dedth ) ACDZAEAFGZHGZEIUAAEJZEUDFGZHGZEIAEAUDIZUCUFE
      UGAUDUBUEHUGKAUDEFLMNUDUAAEOBPQARST $.
      $([14-May-99]$)
  $}

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    divrec.3 $e |- A =/= 0 _10 $.
    $( Relationship between division and reciprocal.  Theorem I.9 of
       Apostol, p. 18. $)
    divrec   $p |- ( B / A ) = ( B x. ( 1 _10 / A ) ) $=
      ( cdiv co cn1 cmulc wceq 1cn divcl mulcl divmul recid mulcoml opreq2i
      mulid1 eqtr mulass1l mpbir ) BAFGBHAFGZIGZJAUCIGBJBAUCDCBUBDHAKCELZMZENUC
      ABUECDBUBABDUDCDBUBAIGZIGBHIGBUFHBIAUBHCUDKACEOPQBDRSTPUA $.
      $([9-Feb-95]$)
  $}

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    ${
      divass.4 $e |- C =/= 0 _10 $.
      $( An associative law for division. $)
      divass   $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cmulc co cdiv cn1 mulcl divrec 1cn divcl opreq2i mulass1r eqtr4 ) ABH
        IZCJISKCJIZHIABCJIZHIZCSFABDELGMABTUBDEKCNFGOAUADBCEFGOLUABTHIAHCBFEGMP
        QR $.
        $([15-Feb-95]$)

      $( A distributive law for division. $)
      divdistr  $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co cn1 cdiv cmulc 1cn divcl distr2 addcl divrec opreq12i
        3eqtr4 ) ABHIZJCKIZLIAUALIZBUALIZHITCKIACKIZBCKIZHIABUADEJCMFGNOCTFABDE
        PGQUDUBUEUCHCAFDGQCBFEGQRS $.
        $([16-Feb-95]$)
    $}

    $( An associative law for division. $)
    divasst $p |- ( C =/= 0 _10 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cn0 wne cmulc co cdiv wceq cn1 cded opreq2 opreq2d cleq12d cc 1cn
      keepel elimne0 divass dedth ) CGHZABIJZCKJZABCKJZIJZLUEUDCMNZKJZABUIKJZIJ
      ZLCMCUILZUFUJUHULCUIUEKOUMUGUKAICUIBKOPQABUIDEUDCMRFSTCUAUBUC $.
      $([12-Aug-99]$)
  $}

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    divcan3.3 $e |- A =/= 0 _10 $.
    $( A cancellation law for division. $)
    divcan3   $p |- ( ( A x. B ) / A ) = B $=
      ( cmulc co cdiv divass divcan2 eqtr ) ABFGAHGABAHGFGBABACDCEIABCDEJK $.
      $([16-Feb-95]$)

    $( A cancellation law for division. $)
    divcan4   $p |- ( ( B x. A ) / A ) = B $=
      ( cmulc co cdiv mulcom opreq1i divcan3 eqtr ) BAFGZAHGABFGZAHGBMNAHBADCIJ
      ABCDEKL $.
      $([18-May-99]$)
  $}

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 _10 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       Apostol, p. 18. $)
    recrec $p |- ( 1 _10 / ( 1 _10 / A ) ) = A $=
      ( cn1 cdiv co cmulc 1cn divcl ax1ne0 divneq0 recid opreq2i opreq1i
      mulid2 eqtr mulass2l mulid1 3eqtr3 ) ADAEFZDTEFZGFZGFADGFUAAUBDAGTDAHBCIZ
      DAHBJCKZLMATUAUABUCDTHUCUDIZUEATGFZUAGFDUAGFUAUFDUAGABCLNUAUEOPQABRS $.
      $([9-Feb-95]$)

    $( A number divided by itself is one. $)
    divid $p |- ( A / A ) = 1 _10 $=
      ( cdiv co cn1 cmulc divrec recid eqtr ) AADEAFADEGEFAABBCHABCIJ $.
      $([9-Feb-95]$)

    $( Division into zero is zero. $)
    divzer $p |- ( 0 _10 / A ) = 0 _10 $=
      ( cn0 cdiv co cn1 cmulc 0cn divrec 1cn divcl mulzer2 eqtr ) DAEFDGAEFZHFD
      ADBICJOGAKBCLMN $.
      $([12-Aug-99]$)
  $}

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 _10 $.
    divmuldiv.6 $e |- D =/= 0 _10 $.
    $( Multiplication of two ratios.  Theorem I.14 of Apostol, p. 18. $)
    divmuldiv   $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cmulc co cdiv mulcl divcl muln0 divcan3 mul4 divcan2 opreq1i opreq2i
      3eqtr eqtr3 ) BDKLZABMLZCDMLZKLZKLZUDMLUGACKLZUDMLUDUGBDFHNUEUFABEFIOZCDG
      HJOZNBDFHIJPQUHUIUDMUHBUEKLZDUFKLZKLAUMKLUIBDUEUFFHUJUKRULAUMKBAFEISTUMCA
      KDCHGJSUAUBTUC $.
      $([16-Feb-95]$)

    divmul13 $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmulc co cdiv mulcom opreq1i divmuldiv 3eqtr4r ) CAKLZBDKLZMLACKLZSMLCB
      MLADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $([6-Aug-99]$)

    $( Addition of two ratios.  Theorem I.13 of Apostol, p. 18. $)
    divadddiv   $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cmulc co caddc cdiv mulcl muln0 divdistr cn1 divid opreq2i divmuldiv
      divcl mulid1 3eqtr3 opreq1i mulid2 opreq12i eqtr2 ) ADKLZBCKLZMLBDKLZNLUI
      UKNLZUJUKNLZMLABNLZCDNLZMLUIUJUKADEHOBCFGOBDFHOBDFHIJPQULUNUMUOMUNDDNLZKL
      UNRKLULUNUPRUNKDHJSTABDDEFHHIJUAUNABEFIUBUCUDBBNLZUOKLRUOKLUMUOUQRUOKBFIS
      UEBBCDFFGHIJUAUOCDGHJUBUFUDUGUH $.
      $([21-Feb-95]$)

    ${
      divdivdiv.7 $e |- C =/= 0 _10 $.
      $( Division of two ratios.  Theorem I.15 of Apostol, p. 18. $)
      divdivdiv   $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
        ( cdiv co cmulc wceq divcl mulcl muln0 divneq0 divmul divmuldiv
        mulcoml opreq2i cn1 mulcom opreq1i divid 3eqtr mulid2 eqtr mulass2l
        eqtr3 mpbir ) ABLMZCDLMZLMADNMZBCNMZLMZOUOURNMZUNOUNUOURABEFIPZCDGHJPZU
        PUQADEHQBCFGQBCFGIKRPZCDGHKJSTUODCLMZUNNMZNMUSUNVDURUONUNVCURUTDCHGKPZV
        BABDCEFHGIKUAUBUCUOVCUNUNVAVEUTUTUOVCNMZUNNMUDUNNMUNVFUDUNNVFCDNMZDCNMZ
        LMVHVHLMUDCDDCGHHGJKUAVGVHVHLCDGHUEUFVHDCHGQDCHGJKRUGUHUFUNUTUIUJUKULUM
        $.
        $([22-Feb-95]$)
    $}
  $}

  ${
    $d x y B $.  $( ` x ` is dummy. $)
    divrcl.1 $e |- A e. RR $.
    divrcl.2 $e |- B e. RR $.
    divrcl.3 $e |- B =/= 0 _10 $.
    $( Closure law for division of reals. $)
    divrcl   $p |- ( A / B ) e. RR $=
      ( vx cdiv co cr wcel cn1 cmulc recn divrec eleq1i cv wceq wrex cn0 wne
      axrrecex mp2 wa wex df-rex cc wb recnt cded cleq2 opreq2 cleq1d bi2bid
      1cn elimel divmul dedth syl eleq1a sylbird imp 19.23aiv sylbi ax-mp
      mulrcl mpbir ) ABGHZIJAKBGHZLHZIJVGVIIBABDMZACMENOAVHCBFPZLHZKQZFIRZVHIJZ
      BIJBSTVNFBUADEUBVNVKIJZVMUCZFUDVOVMFIUEVQVOFVPVMVOVPVMVHVKQZVOVPVKUFJZVRV
      MUGZVKUHVSVTVHVSVKKUIZQZBWALHZKQZUGVKKVKWAQZVRWBVMWDVKWAVHUJWEVLWCKVKWABL
      UKULUMKBWAUNVJVKKUFUNUOEUPUQURVKIVHUSUTVAVBVCVDVEVF $.
      $([9-May-99]$)
  $}

  ${
    divrclt.1 $e |- A e. RR $.
    divrclt.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    divrclt   $p |- ( B =/= 0 _10 -> ( A / B ) e. RR ) $=
      ( cn0 wne cdiv co cr wcel cn1 cded wceq opreq2 eleq1d eleq1 ax1re
      keephyp elimne0 divrcl dedth ) BEFZABGHZIJAUBBKLZGHZIJBKBUDMUCUEIBUDAGNOA
      UDCUBBIJKIJUDIJBKBUDIPKUDIPDQRBSTUA $.
      $([9-May-99]$)
  $}


