$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( Define division, the (unique) element ` x ` such that ` B x = A `.
       This definition is meaningful only when ` B ` is not 0. $)
    df-div $a |- ( A / B ) = U. { x e. CC | ( B x. x ) = A } $.
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for division. $)
    diveq1 $p |- ( A = B -> ( A / C ) = ( B / C ) ) $=
      ( vx wceq cv cmulc co cc crab cuni cdiv cleq2 rbiabdv unieqd df-div
      3eqtr4g ) ABEZCDFGHZAEZDIJZKSBEZDIJZKACLBCLRUAUCRTUBDIABSMNODACPDBCPQ $.
      $([13-Feb-95]$)

    $( Equality theorem for division. $)
    diveq2 $p |- ( A = B -> ( C / A ) = ( C / B ) ) $=
      ( vx wceq cv cmulc co cc crab cuni cdiv opreq1 cleq1d rbiabdv unieqd
      df-div 3eqtr4g ) ABEZADFZGHZCEZDIJZKBTGHZCEZDIJZKCALCBLSUCUFSUBUEDISUAUDC
      ABTGMNOPDCAQDCBQR $.
      $([13-Feb-95]$)
  $}

  ${
    diveqi.1 $e |- A = B $.

    $( Equality inference for division. $)
    diveq1i $p |- ( A / C ) = ( B / C ) $=
      ( wceq cdiv diveq1 ax-mp ) ABEACFBCFEABCGDH $.
      $([15-Feb-95]$)

    $( Equality inference for division. $)
    diveq2i $p |- ( C / A ) = ( C / B ) $=
      ( wceq cdiv diveq2 ax-mp ) ABECAFCBFEABCGDH $.
      $([15-Feb-95]$)

    ${
      diveqi.2 $e |- C = D $.

      $( Equality inference for division. $)
      diveq12i $p |- ( A / C ) = ( B / D ) $=
      ( cdiv diveq1i diveq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
      $([22-Feb-95]$)
    $}
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 _10 $.
    $( Relationship between reciprocal and multiplication. $)
    divmul   $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx cc wcel cdiv wceq cmulc co wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu reuuni receux mpan2 cleqcom df-div cleq2i bitr syl6rbbr
      vtoclga ax-mp ) CIJABKZCLZBCMNZALZOZUNHPZLZBUSMNZALZOURHCIUSCLZUTUOVBUQUS
      CUNQVCVAUPAUSCBMRSTUSIJZVBUSVBHIUAUBZLZUTVDVBHIUCVBVFOVBHIUDHBAEDGUEUFUTU
      SUNLVFUNUSUGUNVEUSHABUHUIUJUKULFUM $.
      $([2-Feb-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 _10 $.
    $( Closure law for division. $)
    divcl   $p |- ( A / B ) e. CC $=
      ( vx cdiv cv cmulc co wceq cc crab cuni df-div wreu wcel reucl receux
      ax-mp eqeltr ) ABGBFHIJAKZFLMNZLFABOUBFLPUCLQUBFLRFBADCESTUA $.
      $([2-Feb-95]$)
  $}

  ${
    divmuleq.1 $e |- A e. CC $.
    divmuleq.2 $e |- B e. CC $.
    divmuleq.3 $e |- A =/= 0 _10 $.
    $( Division and multiplication of equals. $)
    divmuleq   $p |- ( A x. ( B / A ) ) = B $=
      ( cdiv wceq cmulc co divcl divmul cleqid mpbi ) BAFZNGANHIBGBANDCBADCEJEK
      NLM $.
      $([9-Feb-95]$)
  $}

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 _10 $.
    divneq0.4 $e |- B =/= 0 _10 $.
    $( The ratio of non-zero numbers is non-zero. $)
    divneq0   $p |- ( A / B ) =/= 0 _10 $=
      ( cdiv cn0 wne wceq wn cmulc co 0cn divmul mulzer1 cleq1i cleqcom 3bitr
      bineg df-ne 3bitr4 mpbir ) ABGZHIZAHIZUDHJZKAHJZKUEUFUGUHUGBHLMZAJHAJUHAB
      HCDNFOUIHABDPQHARSTUDHUAAHUAUBEUC $.
      $([9-Feb-95]$)
  $}

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 _10 $.
    $( Multiplication of a number and its reciprocal. $)
    recid   $p |- ( A x. ( 1 _10 / A ) ) = 1 _10 $=
      ( cn1 1cn divmuleq ) ADBECF $.
      $([9-Feb-95]$)
  $}

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    divrec.3 $e |- A =/= 0 _10 $.
    $( Relationship between division and reciprocal.  Theorem I.9 of
       Apostol, p. 18. $)
    divrec   $p |- ( B / A ) = ( B x. ( 1 _10 / A ) ) $=
      ( cdiv cn1 cmulc co wceq 1cn divcl mulcl divmul recid mulcoml opreq2i
      mulid1 eqtr mulass1l mpbir ) BAFBGAFZHIZJAUCHIBJBAUCDCBUBDGAKCELZMZENUCAB
      UECDBUBABDUDCDBUBAHIZHIBGHIBUFGBHAUBGCUDKACEOPQBDRSTPUA $.
      $([9-Feb-95]$)
  $}

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    divass.4 $e |- C =/= 0 _10 $.
    $( An associative law for division. $)
    divass   $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
      ( cmulc co cdiv cn1 mulcl divrec 1cn divcl opreq2i mulass1r eqtr4 ) ABHIZ
      CJSKCJZHIABCJZHIZCSFABDELGMABTUBDEKCNFGOAUADBCEFGOLUABTHIAHCBFEGMPQR $.
      $([15-Feb-95]$)

    $( A distributive law for division. $)
    divdistr  $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
      ( caddc co cn1 cdiv cmulc 1cn divcl distr2 addcl divrec opreq12i 3eqtr4
      ) ABHIZJCKZLIAUALIZBUALIZHITCKACKZBCKZHIABUADEJCMFGNOCTFABDEPGQUDUBUEUCHC
      AFDGQCBFEGQRS $.
      $([16-Feb-95]$)
  $}

  ${
    divmuleq2.1 $e |- A e. CC $.
    divmuleq2.2 $e |- B e. CC $.
    divmuleq2.3 $e |- A =/= 0 _10 $.
    $( Division and multiplication of equals. $)
    divmuleq2   $p |- ( ( A x. B ) / A ) = B $=
      ( cmulc co cdiv divass divmuleq eqtr ) ABFGAHABAHFGBABACDCEIABCDEJK $.
      $([16-Feb-95]$)
  $}

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 _10 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       Apostol, p. 18. $)
    recrec   $p |- ( 1 _10 / ( 1 _10 / A ) ) = A $=
      ( cn1 cdiv cmulc co 1cn divcl ax1ne0 divneq0 recid opreq2i opreq1i
      mulid2 eqtr mulass2l mulid1 3eqtr3 ) ADAEZDTEZFGZFGADFGUAAUBDAFTDAHBCIZDA
      HBJCKZLMATUAUABUCDTHUCUDIZUEATFGZUAFGDUAFGUAUFDUAFABCLNUAUEOPQABRS $.
      $([9-Feb-95]$)

    $( A number divided by itself is one. $)
    divid   $p |- ( A / A ) = 1 _10 $=
      ( cdiv cn1 cmulc co divrec recid eqtr ) AADAEADFGEAABBCHABCIJ $.
      $([9-Feb-95]$)

  $}

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 _10 $.
    divmuldiv.6 $e |- D =/= 0 _10 $.
    $( Multiplication of two ratios.  Theorem I.14 of Apostol, p. 18. $)
    divmuldiv   $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cmulc co cdiv mulcl divcl muln0 divmuleq2 mul4 divmuleq opreq1i
      opreq2i 3eqtr diveq1i eqtr3 ) BDKLZABMZCDMZKLZKLZUEMUHACKLZUEMUEUHBDFHNUF
      UGABEFIOZCDGHJOZNBDFHIJPQUIUJUEUIBUFKLZDUGKLZKLAUNKLUJBDUFUGFHUKULRUMAUNK
      BAFEISTUNCAKDCHGJSUAUBUCUD $.
      $([16-Feb-95]$)

    $( Addition of two ratios.  Theorem I.13 of Apostol, p. 18. $)
    divadddiv   $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cmulc co caddc cdiv mulcl muln0 divdistr cn1 divid opreq2i divmuldiv
      divcl mulid1 3eqtr3 opreq1i mulid2 opreq12i eqtr2 ) ADKLZBCKLZMLBDKLZNUIU
      KNZUJUKNZMLABNZCDNZMLUIUJUKADEHOBCFGOBDFHOBDFHIJPQULUNUMUOMUNDDNZKLUNRKLU
      LUNUPRUNKDHJSTABDDEFHHIJUAUNABEFIUBUCUDBBNZUOKLRUOKLUMUOUQRUOKBFISUEBBCDF
      FGHIJUAUOCDGHJUBUFUDUGUH $.
      $([21-Feb-95]$)

    ${
      divdivdiv.7 $e |- C =/= 0 _10 $.
      $( Division of two ratios.  Theorem I.15 of Apostol, p. 18. $)
      divdivdiv   $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
      ( cdiv cmulc co wceq divcl mulcl muln0 divneq0 divmul divmuldiv mulcoml
      opreq2i cn1 mulcom diveq1i divid 3eqtr opreq1i mulid2 eqtr mulass2l
      eqtr3 mpbir ) ABLZCDLZLADMNZBCMNZLZOUPUSMNZUOOUOUPUSABEFIPZCDGHJPZUQURADE
      HQBCFGQBCFGIKRPZCDGHKJSTUPDCLZUOMNZMNUTUOVEUSUPMUOVDUSVADCHGKPZVCABDCEFHG
      IKUAUBUCUPVDUOUOVBVFVAVAUPVDMNZUOMNUDUOMNUOVGUDUOMVGCDMNZDCMNZLVIVILUDCDD
      CGHHGJKUAVHVIVICDGHUEUFVIDCHGQDCHGJKRUGUHUIUOVAUJUKULUMUN $.
      $([22-Feb-95]$)
    $}
  $}


