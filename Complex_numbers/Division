$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Division
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define division.  Theorem ~ divmul relates it to multiplication, and
       ~ divcl and ~ divrcl prove its closure laws. $)
    df-div $a |- / = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. ( CC \ { 0 } ) ) /\
                   z = U. { w e. CC | ( y x. w ) = x } ) } $.
    $(
    Future simpler alternative?
    df-div $a |- / = { <. <. x , y >. , z >. |
                ( ( y e. CC /\ z e. CC ) /\  x = ( y x. z ) ) } $.
    $)
  $}


  ${
    $d x y z w A $.  $d x y z w B $.  $( ` y z w ` are dummy. $)
    divval.1 $e |- A e. CC $.
    divval.2 $e |- B e. CC $.
    divval.3 $e |- B =/= 0 $.
    $( Value of division: the (unique) element ` x ` such that
        ` ( B x. x ) = A ` .  This is meaningful only when ` B ` is nonzero. $)
    divval $p |- ( A / B ) = U. { x e. CC | ( B x. x ) = A } $=
      ( vy vz vw cc wcel cc0 csn cdif cdiv co cv cmulc wceq crab cuni wn wa
      wne df-ne mpbi elisseti elsnc mtbir pm3.2i eldif mpbir axcnex zfausrab
      zfun2 cleq2 birabsdv unieqd opreq1 cleq1d df-div oprabval2 mp2an ) BJKCJL
      MZNZKZBCOPCAQZRPZBSZAJTZUAZSDVFCJKZCVDKZUBZUCVLVNEVMCLSZCLUDVOUBFCLUEUFCL
      CJEUGUHUIUJCJVDUKULGHIBCJVEHQZVGRPZGQZSZAJTZUAVKOVQBSZAJTZUAVJVIAJUMUNUOV
      RBSZVTWBWCVSWAAJVRBVQUPUQURVPCSZWBVJWDWAVIAJWDVQVHBVPCVGRUSUTUQURGHIAVAVB
      VC $.
      $([8-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    divmul.1 $e |- A e. CC $.
    divmul.2 $e |- B e. CC $.
    divmul.3 $e |- C e. CC $.
    divmul.4 $e |- B =/= 0 $.
    $( Relationship between division and multiplication. $)
    divmul   $p |- ( ( A / B ) = C <-> ( B x. C ) = A ) $=
      ( vx cc wcel cdiv co wceq cmulc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu receu reuuni mpan2 cleqcom divval cleq2i bitr syl6rbbr vtoclga
      ax-mp ) CIJABKLZCMZBCNLZAMZOZFUNHPZMZBUSNLZAMZOURHCIUSCMZUTUOVBUQUSCUNQVC
      VAUPAUSCBNRSTUSIJZVBUSVBHIUAUBZMZUTVDVBHIUCVBVFOHBAEDGUDVBHIUEUFUTUSUNMVF
      UNUSUGUNVEUSHABDEGUHUIUJUKULUM $.
      $([2-Feb-95]$)
  $}

  ${
    divmulz.1 $e |- A e. CC $.
    divmulz.2 $e |- B e. CC $.
    divmulz.3 $e |- C e. CC $.
    $( Relationship between division and multiplication. $)
    divmulz   $p |- ( B =/= 0 -> ( ( A / B ) = C <-> ( B x. C ) = A ) ) $=
      ( cc0 wne cdiv co wceq cmulc wb c1 cded opreq2 cleq1d opreq1 bi2bid cc
      1cn keepel elimne0 divmul dedth ) BGHZABIJZCKZBCLJZAKZMAUFBNOZIJZCKZUKCLJ
      ZAKZMBNBUKKZUHUMUJUOUPUGULCBUKAIPQUPUIUNABUKCLRQSAUKCDUFBNTEUAUBFBUCUDUE
      $.
      $([8-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    divcl.1 $e |- A e. CC $.
    divcl.2 $e |- B e. CC $.
    divcl.3 $e |- B =/= 0 $.
    $( Closure law for division. $)
    divcl   $p |- ( A / B ) e. CC $=
      ( vx cdiv co cv cmulc wceq cc crab cuni divval wreu wcel receu reucl
      ax-mp eqeltr ) ABGHBFIJHAKZFLMNZLFABCDEOUBFLPUCLQFBADCERUBFLSTUA $.
      $([2-Feb-95]$)
  $}

  ${
    divclz.1 $e |- A e. CC $.
    divclz.2 $e |- B e. CC $.
    $( Closure law for division. $)
    divclz   $p |- ( B =/= 0 -> ( A / B ) e. CC ) $=
      ( cc0 wne cdiv co cc wcel c1 cded wceq opreq2 eleq1d 1cn keepel elimne0
      divcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKIDPQBRST
      $.
      $([7-May-99]$)
  $}

  $( Closure law for division. $)
  divclt   $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( B =/= 0 -> ( A / B ) e. CC ) ) $=
    ( cc wcel cc0 wne cdiv co wi cded wceq opreq1 eleq1d bilimd neeq1 opreq2
    bi2imd 0cn elimel divclz dedth2h ) ACDZBCDZBEFZABGHZCDZIUDUBAEJZBGHZCDZIUCB
    EJZEFZUGUJGHZCDZIABEEAUGKZUFUIUDUNUEUHCAUGBGLMNBUJKZUDUKUIUMBUJEOUOUHULCBUJ
    UGGPMQUGUJAECRSBECRSTUA $.
    $([21-Jul-01]$)

  ${
    divcan.1 $e |- A e. CC $.
    divcan.2 $e |- B e. CC $.
    ${
      divcan.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan2   $p |- ( A x. ( B / A ) ) = B $=
        ( cdiv co wceq cmulc cleqid divcl divmul mpbi ) BAFGZNHANIGBHNJBANDCBAD
        CEKELM $.
        $([9-Feb-95]$)

      $( A cancellation law for division. $)
      divcan1   $p |- ( ( B / A ) x. A ) = B $=
        ( cdiv co cmulc divcl mulcom divcan2 eqtr ) BAFGZAHGAMHGBMABADCEICJABCD
        EKL $.
        $([18-May-99]$)
    $}

    $( A cancellation law for division. $)
    divcan1z $p |- ( A =/= 0 -> ( ( B / A ) x. A ) = B ) $=
      ( cc0 wne cdiv co cmulc wceq c1 cded opreq2 id opreq12d cleq1d cc 1cn
      keepel elimne0 divcan1 dedth ) AEFZBAGHZAIHZBJBUCAKLZGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $([2-Oct-99]$)

    $( A cancellation law for division. $)
    divcan2z $p |- ( A =/= 0 -> ( A x. ( B / A ) ) = B ) $=
      ( cc0 wne cdiv co cmulc wceq c1 cded id opreq2 opreq12d cleq1d cc 1cn
      keepel elimne0 divcan2 dedth ) AEFZABAGHZIHZBJUCAKLZBUFGHZIHZBJAKAUFJZUEU
      HBUIAUFUDUGIUIMAUFBGNOPUFBUCAKQCRSDATUAUB $.
      $([10-Aug-99]$)
  $}

  $( A cancellation law for division. $)
  divcan2t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A =/= 0 -> ( A x. ( B / A ) ) = B ) ) $=
    ( cc wcel cc0 wne cdiv co cmulc wceq wi cded neeq1 id opreq2 opreq12d
    cleq1d bi2imd opreq1 opreq2d cleq12d bilimd 0cn elimel divcan2z dedth2h )
    ACDZBCDZAEFZABAGHZIHZBJZKUGAELZEFZUMBUMGHZIHZBJZKUNUMUHBELZUMGHZIHZURJZKABE
    EAUMJZUIUNULUQAUMEMVBUKUPBVBAUMUJUOIVBNAUMBGOPQRBURJZUQVAUNVCUPUTBURVCUOUSU
    MIBURUMGSTVCNUAUBUMURAECUCUDBECUCUDUEUF $.
    $([?]$)

  ${
    divneq0.1 $e |- A e. CC $.
    divneq0.2 $e |- B e. CC $.
    divneq0.3 $e |- A =/= 0 $.
    divneq0.4 $e |- B =/= 0 $.
    $( The ratio of non-zero numbers is non-zero. $)
    divneq0   $p |- ( A / B ) =/= 0 $=
      ( cdiv co cc0 wne wceq wn cmulc 0cn divmul mulzer1 cleq1i cleqcom 3bitr
      bineg df-ne 3bitr4 mpbir ) ABGHZIJZAIJZEUDIKZLAIKZLUEUFUGUHUGBIMHZAKIAKUH
      ABICDNFOUIIABDPQIARSTUDIUAAIUAUBUC $.
      $([9-Feb-95]$)
  $}

  ${
    recneq0z.1 $e |- A e. CC $.
    $( The reciprocal of a non-zero number is non-zero. $)
    recneq0z $p |- ( A =/= 0 -> ( 1 / A ) =/= 0 ) $=
      ( cc0 wne c1 cdiv co cded wceq opreq2 neeq1d 1cn cc keepel ax1ne0
      elimne0 divneq0 dedth ) ACDZEAFGZCDESAEHZFGZCDAEAUAITUBCAUAEFJKEUALSAEMBL
      NOAPQR $.
      $([14-May-99]$)
  $}

  ${
    recid.1 $e |- A e. CC $.
    recid.2 $e |- A =/= 0 $.
    $( Multiplication of a number and its reciprocal. $)
    recid   $p |- ( A x. ( 1 / A ) ) = 1 $=
      ( c1 1cn divcan2 ) ADBECF $.
      $([9-Feb-95]$)
  $}

  ${
    recidz.1 $e |- A e. CC $.
    $( Multiplication of a number and its reciprocal. $)
    recidz   $p |- ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) $=
      ( c1 1cn divcan2z ) ACBDE $.
      $([14-May-99]$)
  $}

  $( Multiplication of a number and its reciprocal. $)
  recidt $p |- ( A e. CC -> ( A =/= 0 -> ( A x. ( 1 / A ) ) = 1 ) ) $=
    ( cc wcel cc0 wne c1 cdiv co cmulc wceq wi cded neeq1 id opreq2 opreq12d
    cleq1d bi2imd 0cn elimel recidz dedth ) ABCZADEZAFAGHZIHZFJZKUCADLZDEZUHFUH
    GHZIHZFJZKADAUHJZUDUIUGULAUHDMUMUFUKFUMAUHUEUJIUMNAUHFGOPQRUHADBSTUAUB $.
    $([25-Oct-99]$)

  ${
    divrec.1 $e |- A e. CC $.
    divrec.2 $e |- B e. CC $.
    ${
      divrec.3 $e |- A =/= 0 $.
      $( Relationship between division and reciprocal.  Theorem I.9 of
         [Apostol] p. 18. $)
      divrec   $p |- ( B / A ) = ( B x. ( 1 / A ) ) $=
        ( cdiv co c1 cmulc wceq 1cn divcl mulcl mulcom mulass divcan1 opreq2i
        mulid1 eqtr 3eqtr divmul mpbir ) BAFGBHAFGZIGZJAUDIGZBJUEUDAIGBUCAIGZIG
        ZBAUDCBUCDHAKCELZMZNBUCADUHCOUGBHIGBUFHBIAHCKEPQBDRSTBAUDDCUIEUAUB $.
        $([9-Feb-95]$)
    $}

    $( Relationship between division and reciprocal.  Theorem I.9 of
       [Apostol] p. 18. $)
    divrecz $p |- ( A =/= 0 -> ( B / A ) = ( B x. ( 1 / A ) ) ) $=
      ( cc0 wne cdiv co c1 cmulc wceq cded opreq2 opreq2d cleq12d cc 1cn
      keepel elimne0 divrec dedth ) AEFZBAGHZBIAGHZJHZKBUBAILZGHZBIUFGHZJHZKAIA
      UFKZUCUGUEUIAUFBGMUJUDUHBJAUFIGMNOUFBUBAIPCQRDASTUA $.
      $([11-Oct-99]$)
  $}

  ${
    divass.1 $e |- A e. CC $.
    divass.2 $e |- B e. CC $.
    divass.3 $e |- C e. CC $.
    ${
      divass.4 $e |- C =/= 0 $.
      $( An associative law for division. $)
      divass   $p |- ( ( A x. B ) / C ) = ( A x. ( B / C ) ) $=
        ( cmulc co c1 cdiv 1cn divcl mulass mulcl divrec opreq2i 3eqtr4 ) ABHIZ
        JCKIZHIABTHIZHISCKIABCKIZHIABTDEJCLFGMNCSFABDEOGPUBUAAHCBFEGPQR $.
        $([15-Feb-95]$)

      $( A distributive law for division. $)
      divdistr  $p |- ( ( A + B ) / C ) = ( ( A / C ) + ( B / C ) ) $=
        ( caddc co c1 cdiv cmulc 1cn divcl distr2 addcl divrec opreq12i 3eqtr4
        ) ABHIZJCKIZLIAUALIZBUALIZHITCKIACKIZBCKIZHIABUADEJCMFGNOCTFABDEPGQUDUB
        UEUCHCAFDGQCBFEGQRS $.
        $([16-Feb-95]$)

      $( An associative/distributive law for division. $)
      div23 $p |- ( ( A x. B ) / C ) = ( ( A / C ) x. B ) $=
        ( cmulc co cdiv mulcom opreq1i divass divcl 3eqtr ) ABHIZCJIBAHIZCJIBAC
        JIZHIRBHIPQCJABDEKLBACEDFGMBREACDFGNKO $.
        $([3-Sep-99]$)
    $}

    $( An associative law for division. $)
    divassz $p |- ( C =/= 0 -> ( ( A x. B ) / C ) = ( A x. ( B / C ) ) ) $=
      ( cc0 wne cmulc co cdiv wceq c1 cded opreq2 opreq2d cleq12d cc 1cn
      keepel elimne0 divass dedth ) CGHZABIJZCKJZABCKJZIJZLUEUDCMNZKJZABUIKJZIJ
      ZLCMCUILZUFUJUHULCUIUEKOUMUGUKAICUIBKOPQABUIDEUDCMRFSTCUAUBUC $.
      $([12-Aug-99]$)
  $}

  ${
    divcan3.1 $e |- A e. CC $.
    divcan3.2 $e |- B e. CC $.
    ${
      divcan3.3 $e |- A =/= 0 $.
      $( A cancellation law for division. $)
      divcan3   $p |- ( ( A x. B ) / A ) = B $=
        ( cmulc co cdiv divass divcan2 eqtr ) ABFGAHGABAHGFGBABACDCEIABCDEJK $.
        $([16-Feb-95]$)

      $( A cancellation law for division. $)
      divcan4   $p |- ( ( B x. A ) / A ) = B $=
        ( cmulc co cdiv mulcom opreq1i divcan3 eqtr ) BAFGZAHGABFGZAHGBMNAHBADC
        IJABCDEKL $.
        $([18-May-99]$)
    $}

    $( A cancellation law for division.  (Eliminates a hypothesis of
       ~ divcan3 with the weak deduction theorem.) $)
    divcan3z $p |- ( A =/= 0 -> ( ( A x. B ) / A ) = B ) $=
      ( cc0 wne cmulc co cdiv wceq c1 cded opreq1 id opreq12d cleq1d cc 1cn
      keepel elimne0 divcan3 dedth ) AEFZABGHZAIHZBJUCAKLZBGHZUFIHZBJAKAUFJZUEU
      HBUIUDUGAUFIAUFBGMUINOPUFBUCAKQCRSDATUAUB $.
      $([4-Feb-04]$)

    $( A cancellation law for division. $)
    divcan4z $p |- ( A =/= 0 -> ( ( B x. A ) / A ) = B ) $=
      ( cc0 wne cmulc co cdiv divcan3z mulcom opreq1i syl5eq ) AEFABGHZAIHBBAGH
      ZAIHABCDJONAIBADCKLM $.
      $([12-Oct-99]$)

  $}

  $( A cancellation law for division. $)
  divcan3t $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( A =/= 0 -> ( ( A x. B ) / A ) = B ) ) $=
    ( cc wcel cc0 wne cmulc co cdiv wceq wi cded neeq1 opreq1 id opreq12d
    cleq1d bi2imd opreq2 opreq1d cleq12d bilimd 0cn elimel divcan3z dedth2h )
    ACDZBCDZAEFZABGHZAIHZBJZKUGAELZEFZUMBGHZUMIHZBJZKUNUMUHBELZGHZUMIHZURJZKABE
    EAUMJZUIUNULUQAUMEMVBUKUPBVBUJUOAUMIAUMBGNVBOPQRBURJZUQVAUNVCUPUTBURVCUOUSU
    MIBURUMGSTVCOUAUBUMURAECUCUDBECUCUDUEUF $.
    $([?]$)

  ${
    div11.1 $e |- A e. CC $.
    div11.2 $e |- B e. CC $.
    div11.3 $e |- C e. CC $.
    div11.4 $e |- C =/= 0 $.
    $( One-to-one relationship for division. $)
    div11 $p |- ( ( A / C ) = ( B / C ) <-> A = B ) $=
      ( cdiv co wceq cmulc divcl mulcan divcan2 cleq12i bitr3 ) ACHIZBCHIZJCQKI
      ZCRKIZJABJCQRFACDFGLBCEFGLGMSATBCAFDGNCBFEGNOP $.
      $([20-Aug-01]$)
  $}

  ${
    recrec.1 $e |- A e. CC $.
    recrec.2 $e |- A =/= 0 $.
    $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
       [Apostol] p. 18. $)
    recrec $p |- ( 1 / ( 1 / A ) ) = A $=
      ( c1 cdiv co cmulc 1cn divcl ax1ne0 divneq0 recid opreq2i opreq1i mulass
      mulid2 3eqtr3 mulid1 ) ADAEFZDSEFZGFZGFZADGFTAUADAGSDAHBCIZDAHBJCKZLMASGF
      ZTGFDTGFUBTUEDTGABCLNASTBUCDSHUCUDIZOTUFPQABRQ $.
      $([9-Feb-95]$)

    $( A number divided by itself is one. $)
    divid $p |- ( A / A ) = 1 $=
      ( cdiv co c1 cmulc divrec recid eqtr ) AADEAFADEGEFAABBCHABCIJ $.
      $([9-Feb-95]$)

    $( Division into zero is zero. $)
    divzer $p |- ( 0 / A ) = 0 $=
      ( cc0 cdiv co c1 cmulc 0cn divrec 1cn divcl mulzer2 eqtr ) DAEFDGAEFZHFDA
      DBICJOGAKBCLMN $.
      $([12-Aug-99]$)
  $}


  ${
    div1.1 $e |- A e. CC $.
    $( A number divided by 1 is itself. $)
    div1 $p |- ( A / 1 ) = A $=
      ( c1 cdiv co wceq cmulc mulid2 1cn ax1ne0 divmul mpbir ) ACDEAFCAGEAFABHA
      CABIBJKL $.
      $([9-Jan-02]$)
  $}

  $( A number divided by 1 is itself. $)
  div1t $p |- ( A e. CC -> ( A / 1 ) = A ) $=
    ( cc wcel c1 cdiv co wceq cded opreq1 id cleq12d 1cn elimel div1 dedth ) AB
    CZADEFZAGPADHZDEFZRGADARGZQSARARDEITJKRADBLMNO $.
    $([9-Jan-02]$)

  $( A number is equal to the reciprocal of its reciprocal.  Theorem I.10 of
     [Apostol] p. 18. $)
  recrect $p |- ( ( A e. CC /\ A =/= 0 ) -> ( 1 / ( 1 / A ) ) = A ) $=
    ( cc wcel cc0 wne wa c1 cdiv co wceq cded opreq2 opreq2d id cleq12d eleq1
    neeq1 bi2and 1cn ax1ne0 pm3.2i elimhyp pm3.26i pm3.27i recrec dedth ) ABCZA
    DEZFZGGAHIZHIZAJGGUIAGKZHIZHIZULJAGAULJZUKUNAULUOUJUMGHAULGHLMUONOULULBCZUL
    DEZUIUPUQFGBCZGDEZFAGUOUGUPUHUQAULBPAULDQRGULJURUPUSUQGULBPGULDQRURUSSTUAUB
    ZUCUPUQUTUDUEUF $.
    $([26-Sep-99]$)

  ${
    rec11.1 $e |- A e. CC $.
    rec11.2 $e |- B e. CC $.
    ${
      rec11i.3 $e |- A =/= 0 $.
      rec11i.4 $e |- B =/= 0 $.
      $( Reciprocal is one-to-one. $)
      rec11i $p |- ( ( 1 / A ) = ( 1 / B ) <-> A = B ) $=
        ( c1 cdiv co wceq cmulc opreq2 1cn divcl mul23 recid opreq1i mulid2
        3eqtr mulass opreq2i mulid1 cleq12i cleqcom bitr sylib impbi ) GAHIZGBH
        IZJZABJZUJABKIZUHKIZULUIKIZJZUKUHUIULKLUOBAJUKUMBUNAUMAUHKIZBKIGBKIBABU
        HCDGAMCENOUPGBKACEPQBDRSUNABUIKIZKIAGKIAABUICDGBMDFNTUQGAKBDFPUAACUBSUC
        BAUDUEUFABGHLUG $.
        $([16-Sep-99]$)
    $}

    $( Reciprocal is one-to-one. $)
    rec11 $p |- ( ( A =/= 0 /\ B =/= 0 ) ->
               ( ( 1 / A ) = ( 1 / B ) <-> A = B ) ) $=
      ( cc0 wne c1 cdiv co wceq wb cded opreq2 cleq1d cleq1 bi2bid cleq2d
      cleq2 cc 1cn keepel elimne0 rec11i dedth2h ) AEFZBEFZGAHIZGBHIZJZABJZKGUE
      AGLZHIZUHJZUKBJZKULGUFBGLZHIZJZUKUOJZKABGGAUKJZUIUMUJUNUSUGULUHAUKGHMNAUK
      BOPBUOJZUMUQUNURUTUHUPULBUOGHMQBUOUKRPUKUOUEAGSCTUAUFBGSDTUAAUBBUBUCUD $.
      $([16-Sep-99]$)
  $}

  ${
    divmuldiv.1 $e |- A e. CC $.
    divmuldiv.2 $e |- B e. CC $.
    divmuldiv.3 $e |- C e. CC $.
    divmuldiv.4 $e |- D e. CC $.
    divmuldiv.5 $e |- B =/= 0 $.
    divmuldiv.6 $e |- D =/= 0 $.
    $( Multiplication of two ratios.  Theorem I.14 of [Apostol] p. 18. $)
    divmuldiv   $p |- ( ( A / B ) x. ( C / D ) ) =
                      ( ( A x. C ) / ( B x. D ) ) $=
      ( cmulc co cdiv mulcl divcl muln0 divcan3 mul4 divcan2 opreq1i opreq2i
      3eqtr eqtr3 ) BDKLZABMLZCDMLZKLZKLZUDMLUGACKLZUDMLUDUGBDFHNUEUFABEFIOZCDG
      HJOZNBDFHIJPQUHUIUDMUHBUEKLZDUFKLZKLAUMKLUIBDUEUFFHUJUKRULAUMKBAFEISTUMCA
      KDCHGJSUAUBTUC $.
      $([16-Feb-95]$)

    $( Swap denominators of two ratios. $)
    divmul13 $p |- ( ( A / B ) x. ( C / D ) ) =
                   ( ( C / B ) x. ( A / D ) ) $=
      ( cmulc co cdiv mulcom opreq1i divmuldiv 3eqtr4r ) CAKLZBDKLZMLACKLZSMLCB
      MLADMLKLABMLCDMLKLRTSMCAGENOCBADGFEHIJPABCDEFGHIJPQ $.
      $([6-Aug-99]$)

    $( Addition of two ratios.  Theorem I.13 of [Apostol] p. 18. $)
    divadddiv   $p |- ( ( A / B ) + ( C / D ) ) =
                      ( ( ( A x. D ) + ( B x. C ) ) / ( B x. D ) ) $=
      ( cmulc co caddc cdiv mulcl muln0 divdistr c1 divid opreq2i divmuldiv
      divcl mulid1 3eqtr3 opreq1i mulid2 opreq12i eqtr2 ) ADKLZBCKLZMLBDKLZNLUI
      UKNLZUJUKNLZMLABNLZCDNLZMLUIUJUKADEHOBCFGOBDFHOBDFHIJPQULUNUMUOMUNDDNLZKL
      UNRKLULUNUPRUNKDHJSTABDDEFHHIJUAUNABEFIUBUCUDBBNLZUOKLRUOKLUMUOUQRUOKBFIS
      UEBBCDFFGHIJUAUOCDGHJUBUFUDUGUH $.
      $([21-Feb-95]$)

    ${
      divdivdiv.7 $e |- C =/= 0 $.
      $( Division of two ratios.  Theorem I.15 of [Apostol] p. 18. $)
      divdivdiv   $p |- ( ( A / B ) / ( C / D ) )
                        = ( ( A x. D ) / ( B x. C ) ) $=
        ( cdiv co cmulc wceq divcl mulcom divmuldiv eqtr opreq2i c1 opreq1i
        mulcl muln0 divid 3eqtr mulass mulid2 3eqtr3 eqtr3 divneq0 divmul
        mpbir ) ABLMZCDLMZLMADNMZBCNMZLMZOUOURNMZUNOUODCLMZUNNMZNMZUSUNVAURUONV
        AUNUTNMURUTUNDCHGKPZABEFIPZQABDCEFHGIKRSTUOUTNMZUNNMUAUNNMVBUNVEUAUNNVE
        CDNMZDCNMZLMVGVGLMUACDDCGHHGJKRVFVGVGLCDGHQUBVGDCHGUCDCHGJKUDUEUFUBUOUT
        UNCDGHJPZVCVDUGUNVDUHUIUJUNUOURVDVHUPUQADEHUCBCFGUCBCFGIKUDPCDGHKJUKULU
        M $.
        $([22-Feb-95]$)
    $}
  $}

  ${
    divdiv23.1 $e |- A e. CC $.
    divdiv23.2 $e |- B e. CC $.
    divdiv23.3 $e |- C e. CC $.
    ${
      divdiv23i.3 $e |- B =/= 0 $.
      divdiv23i.4 $e |- C =/= 0 $.
      $( Swapping denominators in a division. $)
      divdiv23i $p |- ( ( A / B ) / C ) = ( ( A / C ) / B ) $=
        ( c1 cdiv co cmulc 1cn divcl div23 divrec opreq1i 3eqtr4 ) AIBJKZLKZCJK
        ACJKZSLKABJKZCJKUABJKASCDIBMEGNFHOUBTCJBAEDGPQBUAEACDFHNGPR $.
        $([15-Sep-99]$)
    $}
    $( Swapping denominators in a division. $)
    divdiv23 $p |- ( ( B =/= 0 /\ C =/= 0 ) ->
                ( ( A / B ) / C ) = ( ( A / C ) / B ) ) $=
      ( cc0 wne cdiv co wceq c1 cded opreq2 opreq1d cleq12d cc 1cn keepel
      elimne0 divdiv23i dedth2h ) BGHZCGHZABIJZCIJZACIJZBIJZKAUCBLMZIJZCIJZUGUI
      IJZKUJUDCLMZIJZAUMIJZUIIJZKBCLLBUIKZUFUKUHULUQUEUJCIBUIAINOBUIUGINPCUMKZU
      KUNULUPCUMUJINURUGUOUIICUMAINOPAUIUMDUCBLQERSUDCLQFRSBTCTUAUB $.
      $([15-Sep-99]$)
  $}

  ${
    $d x y B $.  $( ` x ` is dummy. $)
    divrcl.1 $e |- A e. RR $.
    divrcl.2 $e |- B e. RR $.
    divrcl.3 $e |- B =/= 0 $.
    $( Closure law for division of reals. $)
    divrcl   $p |- ( A / B ) e. RR $=
      ( vx cdiv co c1 cmulc cr recn divrec cv wceq wrex wcel cc0 wne axrrecex
      mp2 wa wex df-rex cc wb recnt cded cleq2 opreq2 cleq1d bi2bid 1cn elimel
      divmul dedth syl eleq1a sylbird imp 19.23aiv sylbi ax-mp mulrcl eqeltr )
      ABGHAIBGHZJHKBABDLZACLEMAVFCBFNZJHZIOZFKPZVFKQZBKQBRSVKDEFBTUAVKVHKQZVJUB
      ZFUCVLVJFKUDVNVLFVMVJVLVMVJVFVHOZVLVMVHUEQZVOVJUFZVHUGVPVQVFVPVHIUHZOZBVR
      JHZIOZUFVHIVHVROZVOVSVJWAVHVRVFUIWBVIVTIVHVRBJUJUKULIBVRUMVGVHIUEUMUNEUOU
      PUQVHKVFURUSUTVAVBVCVDVE $.
      $([9-May-99]$)
  $}

  ${
    divrclz.1 $e |- A e. RR $.
    divrclz.2 $e |- B e. RR $.
    $( Closure law for division of reals. $)
    divrclz   $p |- ( B =/= 0 -> ( A / B ) e. RR ) $=
      ( cc0 wne cdiv co cr wcel c1 cded wceq opreq2 eleq1d ax1re keepel
      elimne0 divrcl dedth ) BEFZABGHZIJAUABKLZGHZIJBKBUCMUBUDIBUCAGNOAUCCUABKI
      DPQBRST $.
      $([9-May-99]$)
  $}

  $( Closure law for division of reals. $)
  divrclt $p |- ( ( A e. RR /\ B e. RR ) ->
                ( B =/= 0 -> ( A / B ) e. RR ) ) $=
    ( cr wcel cc0 wne cdiv co wi cded wceq opreq1 eleq1d bilimd neeq1 opreq2
    bi2imd ax0re elimel divrclz dedth2h ) ACDZBCDZBEFZABGHZCDZIUDUBAEJZBGHZCDZI
    UCBEJZEFZUGUJGHZCDZIABEEAUGKZUFUIUDUNUEUHCAUGBGLMNBUJKZUDUKUIUMBUJEOUOUHULC
    BUJUGGPMQUGUJAECRSBECRSTUA $.
    $([27-Sep-99]$)


