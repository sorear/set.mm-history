$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integer powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c ^ $. $( Exponentiation. $)

  $( Extend class notation to include exponentiation. $)
  cexp $a class ^ $.

  ${
    $d x y z $.
    $( Define exponentiation to integer powers.  (Currently only the exponent
       ` 2 _10 ` is defined, to allow us to use the square of a number with
       a convenient notation.  In a later version of the Metamath set theory
       database, this will be extended to arbitrary integer exponents using
       finite recursion, but all proofs involving squares will remain the
       same because we will directly reference only theorem ~sqvalt and its
       consequences.) $)
    df-exp $a |- ^ = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. { 2 _10 } ) /\ z = ( x x. x ) ) } $.

  $}

  ${
    $d x y z A $.
    $( Value of square. $)
    sqvalt $p |- ( A e. CC -> ( A ^ 2 _10 ) = ( A x. A ) ) $=
      ( vx vy vz cc wcel cl2 csn cexp co cmulc wceq cv oprex id opreq12d
      cleqid a1i df-exp oprabval2 cr 2re elisseti snid mpan2 ) AEFGGHZFAGIJAAKJ
      ZLBCDAGEUFBMZUHKJUGIUGAAKNUHALZUHAUHAKUIOZUJPUGUGLCMGLUGQRBCDSTGGUAUBUCUD
      UE $.
      $([1-Aug-99]$)
  $}

  sqclt $p |- ( A e. CC -> ( A ^ 2 _10 ) e. CC ) $=
    ( cc wcel cl2 cexp co wa cmulc wceq sqvalt adantr eleq1d axmulcl mpbird
    anabss ) ABCZADEFZBCZPPGZRAAHFZBCSQTBPQTIPAJKLAAMNO $.
    $([10-Aug-99]$)

  ${
    sqval.1 $e |- A e. CC $.
    $( Value of square.  Inference version. $)
    sqval $p |- ( A ^ 2 _10 ) = ( A x. A ) $=
      ( cc wcel cl2 cexp co cmulc wceq sqvalt ax-mp ) ACDAEFGAAHGIBAJK $.
      $([1-Aug-99]$)

    $( Closure of square. $)
    sqcl $p |- ( A ^ 2 _10 ) e. CC $=
      ( cc wcel cl2 cexp co sqclt ax-mp ) ACDAEFGCDBAHI $.
      $([2-Aug-99]$)
  $}

  ${
    sqrecl.1 $e |- A e. RR $.
    $( Closure of square in reals. $)
    sqrecl $p |- ( A ^ 2 _10 ) e. RR $=
      ( cl2 cexp co cmulc cr recn sqval mulrcl eqeltr ) ACDEAAFEGAABHIAABBJK $.
      $([2-Aug-99]$)

    $( A square of a real is non-negative. $)
    sqege0 $p |- 0 _10 <_ ( A ^ 2 _10 ) $=
      ( cn0 cl2 cexp co cle wbr cmulc recn sqval breq2i sqge0 mpbir ) CADEFZGHC
      AAIFZGHOPCGAABJKLABMN $.
      $([3-Aug-99]$)

    $( Absolute value of a real number. $)
    absre $p |- ( abs ` A ) = ( sqr ` ( A ^ 2 _10 ) ) $=
      ( cabs cfv ccj cmulc co csqr cl2 cexp cc wcel wceq recn absval ax-mp
      sqval cr recj mpbi opreq2i eqtr fveq2i eqtr4 ) ACDZAAEDZFGZHDZAIJGZHDAKLU
      EUHMABNZAOPUIUGHUIAAFGUGAUJQAUFAFARLAUFMAUJSBTUAUBUCUD $.
      $([3-Aug-99]$)

    $( Square root of square. $)
    sqrsqe $p |- ( 0 _10 <_ A -> ( sqr ` ( A ^ 2 _10 ) ) = A ) $=
      ( cn0 cle wbr cmulc co csqr cfv cl2 cexp sqrsqid recn sqval fveq2i
      syl5eq ) CADEAAFGZHIAAJKGZHIABLRQHAABMNOP $.
      $([11-Aug-99]$)

    $( Square of square root. $)
    sqsqr $p |- ( 0 _10 <_ A -> ( ( sqr ` A ) ^ 2 _10 ) = A ) $=
      ( cn0 cle wbr csqr cfv cl2 cexp co cmulc cc wcel wceq cr sqrcl recnt syl
      sqvalt sqrth eqtrd ) CADEZAFGZHIJZUCUCKJZAUBUCLMZUDUENUBUCOMUFABPUCQRUCSR
      ABTUA $.
      $([11-Aug-99]$)
  $}


