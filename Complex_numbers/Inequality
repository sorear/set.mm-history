$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Inequality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $. $( ` x y z ` are dummy. $)
    $( "Less than" is a strict ordering. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wbr weq wo wn wb wa wi axlttri 3adant3
      axlttrn jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZABJUCUAEIKLMZUGUCUEEINUAU
      EEIOUBUDUHUFUAUCPQUAUCUERST $.
      $([19-Jan-97]$)
  $}

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.

      $( Derivative of trichotomy. $)
      lttri2 $p |- ( -. A = B <-> ( A < B \/ B < A ) ) $=
      ( clt wbr wo wceq wn cr wor wcel wa wb sotrieq ltso pm3.2i mp2an bicom
      bicon1 ) ABEFBAEFGZABHZUBUAIZJEKAJLZBJLZMUBUCNJABEOPUDUECDQRST $.
      $([19-Jan-97]$)

      ${
        lt.3 $e |- C e. RR $.
        $( Addition to both sides of an inequality. $)
        ltadd2 $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
      ( clt wbr caddc co cr wcel wi axltadd mp3an1 mp2an cneg addrcl negrcl
      cn0 recn 0cn negid addcoml opreq1i addass addid2 3eqtr3 3brtr3g impbi )
      ABGHZCAIJZCBIJZGHZBKLZCKLZUKUNMZAKLUOUPUQABCNDOEFPUNCQZULIJZURUMIJZABGUMK
      LZURKLZUNUSUTGHMZULKLVAVBVCULUMURNCAFDROCBFERCFSZPURCIJZAIJTAIJUSAVETAICU
      RTCFUAZURVDUAZUBCVFUCUDZUEURCAVGVFADUAZUFAVIUGUHVEBIJTBIJUTBVETBIVHUEURCB
      VGVFBEUAZUFBVJUGUHUIUJ $.
      $([21-Jan-97]$)

        $( Addition to both sides of an inequality.  Theorem I.18 of Apostol,
           p. 20. $)
        ltadd1 $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
      ( clt wbr caddc co ltadd2 recn addcom breq12i bitr ) ABGHCAIJZCBIJZGHACIJ
      ZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
      $([21-Jan-97]$)

        $( Inequality relation between subtraction and addition. $)
        ltsubaddOBS $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
      ( cmin clt wbr cneg caddc co recn negrcl subrcl subneg addcomr breq1i
      addrcl ltadd2 cn0 negid opreq1i addass addid2 3eqtr3 3bitr ) ABGZCHIBJZAK
      LZCHIBUJKLZBCKLZHIAULHIUHUJCHAUIUHADMZUIBENZMZUHABDEOMABUMBEMZPQRUJCBUIAU
      NDSFETUKAULHBUIKLZAKLUAAKLUKAUQUAAKBUPUBUCBUIAUPUOUMUDAUMUEUFRUG $.
      $([21-Jan-97]$)

        $( Inequality relation between subtraction and addition. $)
        ltsubadd $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
      ( cmin clt wbr cneg caddc co recn subneg breq1i negrcl addrcl ltadd1 cn0
      ax0re 0cn negid addcoml opreq2i addass1l addid1 eqtr 3bitr ) ABGZCHIABJZK
      LZCHIUKBKLZCBKLZHIAUMHIUIUKCHABADMZBEMZNOUKCBAUJDBEPZQFERULAUMHULASKLZAAU
      JBUQUNUJUPMZUOUQASDTQMUJBKLSAKBUJSUOURUABUOUBUCUDUEAUNUFUGOUH $.
      $([21-Jan-97]$)
      $}

      $( Negation of both sides of an inequality.  Theorem I.23 of Apostol,
         p. 20. $)
      ltneg $p |- ( A < B <-> ( - B ) < ( - A ) ) $=
      ( clt wbr cneg caddc co negrcl addrcl ltadd1 recn cn0 negid opreq1i
      addid2 eqtr addass2l add12 opreq2i addid1 3eqtr breq12i bitr ) ABEFAAGZBG
      ZHIZHIZBUHHIZEFUGUFEFABUHCDUFUGACJZBDJZKLUIUGUJUFEAUFUGUGACMZUFUKMZUGULMZ
      UOAUFHIZUGHINUGHIUGUPNUGHAUMOPUGUOQRSUJUFBUGHIZHIUFNHIUFBUFUGBDMZUNUOTUQN
      UFHBUROUAUFUNUBUCUDUE $.
      $([21-Jan-97]$)
    $}

    $( A non-zero square is positive.  Theorem I.20 of Apostol, p. 20. $)
    ltsq $p |- ( -. A = 0 _10 -> 0 _10 < ( A x. A ) ) $=
      ( cn0 wceq wn clt wbr wo cmulc co ax0re lttri2 cneg ltneg neg0 breq1i
      bitr cr wcel wa wi axmulgt0 negrcl mp2an anabss recn mul2neg breq2i
      sylib sylbi jaoi ) ACDEACFGZCAFGZHCAAIJZFGZACBKLULUOUMULCAMZFGZUOULCMZUPF
      GUQACBKNURCUPFOPQUQCUPUPIJZFGZUOUQUTUPRSZVAUQUQTUTUAUPUPUBABUCZVBUDUEUSUN
      CFAAABUFZVCUGUHUIUJUMUOARSZVDUMUMTUOUAAAUBBBUDUEUKUJ $.
      $([17-Jan-97]$)
  $}

  $( 0 is less than 1.  Theorem I.21 of Apostol, p. 20. $)
  lt01 $p |- 0 _10 < 1 _10 $=
      ( cn0 cn1 cmulc co clt wbr 1cn mulid1 breq2i wceq wn ax1re ltsq wne
      df-ne ax1ne0 mpbi ax-mp ) ABBCDZEFZABEFSBAEBGHIBAJKZTBLMBANUABAOPQRQ $.
      $([17-Jan-97]$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Inequality relation between subtraction and addition. $)
    ltsubaddtOBS $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
      ( vx cmin clt wbr caddc co wb cv cn0 wcel cr wi wa cab wceq subeq1
      breq1d breq1 bi2bid subeq2 opreq1 breq2d breq2 opreq2 ax0re cldedg2
      ltsubaddOBS cldedg33 ) ABEZCFGZABCHIZFGZJDKZLMZANMZOUPAMURPODQZBEZCFGZUSUNFG
      ZJUSUQBNMZOUPBMVCPODQZEZCFGZUSVDCHIZFGZJVEUQCNMZOUPCMVIPODQZFGZUSVDVJHIZF
      GZJDABCLNAUSRZUMVAUOVBVNULUTCFAUSBSTAUSUNFUAUBBVDRZVAVFVBVHVOUTVECFBVDUSU
      CTVOUNVGUSFBVDCHUDUEUBCVJRZVFVKVHVMCVJVEFUFVPVGVLUSFCVJVDHUGUEUBUSVDVJDAN
      LUHUIDBNLUHUIDCNLUHUIUJUK $.
      $([21-Jan-97]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Inequality relation between subtraction and addition. $)
    ltsubaddt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
      ( vx cmin clt wbr caddc co wb cv cn0 wcel cr wi wa cab wceq subeq1
      breq1d breq1 bi2bid subeq2 opreq2 breq2d breq2 opreq1 ax0re cldedg2
      ltsubadd cldedg33 ) ABEZCFGZACBHIZFGZJDKZLMZANMZOUPAMURPODQZBEZCFGZUSUNFG
      ZJUSUQBNMZOUPBMVCPODQZEZCFGZUSCVDHIZFGZJVEUQCNMZOUPCMVIPODQZFGZUSVJVDHIZF
      GZJDABCLNAUSRZUMVAUOVBVNULUTCFAUSBSTAUSUNFUAUBBVDRZVAVFVBVHVOUTVECFBVDUSU
      CTVOUNVGUSFBVDCHUDUEUBCVJRZVFVKVHVMCVJVEFUFVPVGVLUSFCVJVDHUGUEUBUSVDVJDAN
      LUHUIDBNLUHUIDCNLUHUIUJUK $.
      $([21-Jan-97]$)
  $}

