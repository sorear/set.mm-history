$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4   $p |- ( ( A  x. B )  x. ( C  x. D ) ) =
                 ( ( A  x. C )  x. ( B  x. D ) ) $=
      ( cmulc co mulcom opreq1i mulass 3eqtr3 opreq2i mulcl 3eqtr4 ) ABCDIJZIJZ
      IJACBDIJZIJZIJABIJRIJACIJTIJSUAAIBCIJZDIJCBIJZDIJSUAUBUCDIBCFGKLBCDFGHMCB
      DGFHMNOABREFCDGHPMACTEGBDFHPMQ $.
      $([16-Feb-95]$)
  $}

  ${
    subdist.1 $e |- A e. CC $.
    subdist.2 $e |- B e. CC $.
    subdist.3 $e |- C e. CC $.
    $( Distribution of muliplication over subtraction.  Theorem I.5 of
       Apostol, p. 18. $)
    subdist   $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cmulc co cmin wceq caddc mulcl subcl subadd distr1 subaddeq opreq2i
      eqtr3 mpbir cleqcomi ) ABGHZACGHZIZABCIZGHZUCUEJUBUEKHZUAJUAUBUEABDELACDF
      LAUDDBCEFMZLNACUDKHZGHUFUAACUDDFUGOUHBAGCBFEPQRST $.
      $([26-Nov-94]$)
  $}

  ${
    mulzer.1 $e |- A e. CC $.

    $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
    mulzer1  $p |- ( A x. 0 _10 ) = 0 _10 $=
      ( cn0 cmin cmulc co 0cn subdist subid opreq2i mulcl 3eqtr3 ) ACCDZEFACEFZ
      NDNCACCBGGHMCAECGIJNACBGKIL $.
      $([23-Nov-94]$)

    $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
    mulzer2  $p |- ( 0 _10 x. A ) = 0 _10 $=
      ( cn0 0cn mulzer1 mulcoml ) ACCBDDABEF $.
      $([23-Nov-94]$)
  $}

  ${
    mulneg.1 $e |- A e. CC $.
    mulneg.2 $e |- B e. CC $.

    $( Product with negative is negative of product.  Theorem I.12 of Apostol,
       p. 18. $)
    mulneg   $p |- ( ( - A ) x. B ) = ( - ( A x. B ) ) $=
      ( cn0 cmulc co cmin cneg mulzer1 mulcom subeq12i df-neg opreq1i 0cn
      subcl subdist 3eqtr 3eqtr4 ) BEFGZBAFGZHZEABFGZHAIZBFGZUCITEUAUCBDJBADCKL
      UEEAHZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
      $([10-Feb-95]$)

    $( Product of two negatives.  Theorem I.12 of Apostol, p. 18. $)
    mul2neg   $p |- ( ( - A ) x. ( - B ) ) = ( A x. B ) $=
      ( cneg cmulc co negcl mulcl mulneg mulcoml negeqi negneg 3eqtr mulcomr )
      BAAEZBEZFGZDCPQACHBDHZIRAQFGZEBAFGZEZEUAAQCSJTUBQAUBSCUABADCIZHBADCJKLUAU
      CMNO $.
      $([14-Feb-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- A =/= 0 _10 $.
    $( Cancellation law for multiplication.  Theorem I.7 of Apostol, p. 18. $)
    mulcan   $p |- ( ( A x. B ) = ( A x. C ) -> B = C ) $=
      ( vx cv cmulc co cn1 wceq cc wrex wi wcel wa wb axmulass mp3an3 cleq12d
      mpan2 opreq2 syl5bir adantr axmulcom mpan cleq1d opreq1 mulid2 syl6eq
      syl6bi imp sylibd exp r19.23aiv recex ax-mp ) AHIZJKZLMZHNOABJKZACJKZMZB
      CMZPZVBVGHNUTNQZVBVGVHVBRVEUTAJKZBJKZVICJKZMZVFVHVEVLPVBVHVLUTVCJKZUTVDJK
      ZMZVEVHANQZVLVOSVHVPRVJVMVKVNVHVPBNQVJVMMUTABTEUAVHVPCNQVKVNMUTACTFUAUBDU
      CVCVDUTJUDUEUFVHVBVLVFSZVHVBVILMZVQVHVAVILVPVHVAVIMAUTUGDUHUIVRVJBVKCVRVJ
      LBJKBVILBJUJBEUKULVRVKLCJKCVILCJUJCFUKULUBUMUNUOUPUQHADGURUS $.
      $([26-Jan-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    mulcant.1 $e |- A =/= 0 _10 $.
    $( Cancellation law for multiplication (theorem form).  Theorem I.7
       of Apostol, p. 18.  Illustrates use of ~cldedgg4 . $)
    mulcant   $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A x. B ) = ( A x. C ) -> B = C ) ) $=
      ( vx cmulc co wceq wi cv cn1 wcel cc wa cab opreq1 cleq12d birimd
      opreq2 cleq1d cleq1 bi2imd cleq2d cleq2 1cn cldedg2 cn0 wne neeq1
      ax1ne0 cldedgg4 mulcan cldedg33 ) ABFGZACFGZHZBCHZIEJZKLZAMLZIURALUTNIEOZ
      BFGZVACFGZHZUQIVAUSBMLZIURBLVENIEOZFGZVCHZVFCHZIVGVAUSCMLZIURCLVJNIEOZFGZ
      HZVFVKHZIEABCKMAVAHZUPVDUQVOUNVBUOVCAVABFPAVACFPQRBVFHZVDVHUQVIVPVBVGVCBV
      FVAFSTBVFCUAUBCVKHZVHVMVIVNVQVCVLVGCVKVAFSUCCVKVFUDUBVAVFVKEAMKUEUFEBMKUE
      UFECMKUEUFAUGUHKUGUHVAUGUHUTEAKAVAUGUIKVAUGUIDUJUKULUM $.
      $([29-Jan-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    $( Cancellation law for multiplication (full theorem form).  Theorem I.7
       of Apostol, p. 18.  Illustrates use of ~cldedgg1 and ~cldedgg2 . $)
    mulcant2   $p |- ( ( A =/= 0 _10 /\ ( A e. CC /\ B e. CC /\ C e. CC ) ) ->
                   ( ( A x. B ) = ( A x. C ) -> B = C ) ) $=
      ( vx cn0 wne cc wcel w3a cmulc co wceq wi cv cn1 wa cab cleq3 pm4.2i
      bi3and opreq1 cleq12d birimd bi2imd neeq1 ax1ne0 cldedgg2 mulcant
      cldedgg3 imp ) AEFZAGHZBGHZCGHZIZABJKZACJKZLZBCLZMZUKUOUTMDNZOHUKMVAAHUKP
      MDQZGHZUMUNIZVBBJKZVBCJKZLZUSMZMDAOAVBLZUOVDUTVHVIULVCUMUMUNUNAVBGRVIUMSV
      IUNSTVIURVGUSVIUPVEUQVFAVBBJUAAVBCJUAUBUCUDVBBCUKVBEFOEFDAOAVBEUEOVBEUEUF
      UGUHUIUJ $.
      $([29-Jan-95]$)
  $}

  ${
    mul0or.1 $e |- A e. CC $.
    mul0or.2 $e |- B e. CC $.
    $( If a product is zero, one of its factors must be zero.  Theorem I.11 of
       Apostol, p. 18. $)
    mul0or   $p |- ( ( A x. B ) = 0 _10 -> ( A = 0 _10 \/ B = 0 _10 ) ) $=
      ( cmulc co cn0 wceq wn wne wi df-ne cc wcel w3a mulcant2 0cn 3pm3.2i
      mpan2 mulzer1 cleq2i syl5ibr sylbir comm orrd ) ABEFZGHZAGHZBGHZUHIZUGUI
      UJAGJZUGUIKAGLUKUFAGEFZHZUIUGUKAMNZBMNZGMNZOUMUIKABGPUNUOUPCDQRSULGUFACTU
      AUBUCUDUE $.
      $([10-Feb-95]$)

    ${
      muln0.3 $e |- A =/= 0 _10 $.
      muln0.4 $e |- B =/= 0 _10 $.
      $( The product of two non-zero numbers is non-zero. $)
      muln0   $p |- ( A x. B ) =/= 0 _10 $=
      ( cn0 wne cmulc co wceq wn wi df-ne wa wo ioran mul0or con3 sylbir exp
      sylbi 3imtr4g mp2 ) AGHZBGHZABIJZGHZUEBGKZLZUGGKZLZUFUHUEAGKZLZUJULMAGNUN
      UJULUNUJOUMUIPZLULUMUIQUKUOABCDRSTUAUBBGNUGGNUCEFUD $.
      $([15-Feb-95]$)
    $}
  $}

  ${
    $d x y A $.  $d x y B $. $( ` y ` is dummy. $)
    receux.1 $e |- A e. CC $.
    receux.2 $e |- B e. CC $.
    receux.3 $e |- A =/= 0 _10 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of Apostol,
       p. 18. $)
    receux   $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmulc co wceq cc wreu wrex wa weq wi wral opreq2 cleq1d reu4
      cn1 wcel axmulass mp3an1 mpan2 cleqcomd sylan9req opreq1 mulid2 syl6eq
      exp32 comm imp a1i r19.21aiv exp r19.22 syl6 com23 axmulcl risset sylib
      mpd r19.23aiv recex ax-mp w3a mulcant cleq2 biimparc syl5 rgen2 pm3.2i
      mpbir ) BAHZIJZCKZALMVRALNZVRBGHZIJZCKZOZAGPZQZGLRALRZOVRWBAGLWDVQWACVPVT
      BISTUAVSWFWAUBKZGLNVSWGVSGLVTLUCZVPVTCIJZKZALNZWGVSQWHWGWKVSWHWGWJVRQZALR
      ZWKVSQWHWGWMWHWGOZWLALWNWLQVPLUCZWHWGWLWGWHWLWGWHWJVRWHWJOWGVQWACIJZCWJWH
      VQBWIIJZWPVPWIBISWHWPWQWHCLUCZWPWQKZBLUCZWHWRWSBVTCUDDUEEUFUGUHWGWPUBCIJC
      WAUBCIUICEUJUKUHULUMUNUOUPUQWJVRALURUSUTWHWILUCZWKWHWRXAVTCVAEUFAWILVBVCV
      DVEGBDFVFVGWEAGLWTWOWHWEWTWOWHVHVQWAKZWDWCBVPVTFVIWBXBVRWACVQVJVKVLDUEVMV
      NVO $.
      $([29-Jan-95]$)
  $}


