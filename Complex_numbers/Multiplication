$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Multiplication
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    mul.1 $e |- A e. CC $.
    mul.2 $e |- B e. CC $.
    mul.3 $e |- C e. CC $.
    $( Commutative/associative law. $)
    mul12 $p |- ( A x. ( B x. C ) ) = ( B x. ( A x. C ) ) $=
      ( cmulc co mulcl mulcom opreq1i mulass2r mulass2l ) ABCBACGHZGHDEFBNEACDF
      IIBACABGHZCGHEDFOCABDEIFIOBAGHCGABDEJKLM $.
      $([11-May-99]$)

    $( Commutative/associative law. $)
    mul23 $p |- ( ( A x. B ) x. C ) = ( ( A x. C ) x. B ) $=
      ( cmulc co mulcl mulcom opreq2i mulass1r mulass1l ) ABCACGHZBGHDEFNBACDFI
      EIACBABCGHZGHDFEAODBCEFIIOCBGHAGBCEFJKLM $.
      $([11-May-99]$)
  $}

  ${
    mul4.1 $e |- A e. CC $.
    mul4.2 $e |- B e. CC $.
    mul4.3 $e |- C e. CC $.
    mul4.4 $e |- D e. CC $.
    $( Rearrangement of 4 factors. $)
    mul4   $p |- ( ( A  x. B )  x. ( C  x. D ) ) =
                 ( ( A  x. C )  x. ( B  x. D ) ) $=
      ( cmulc co mulcom opreq1i mulass 3eqtr3 opreq2i mulcl 3eqtr4 ) ABCDIJZIJZ
      IJACBDIJZIJZIJABIJRIJACIJTIJSUAAIBCIJZDIJCBIJZDIJSUAUBUCDIBCFGKLBCDFGHMCB
      DGFHMNOABREFCDGHPMACTEGBDFHPMQ $.
      $([16-Feb-95]$)

    $( Product of sums. $)
    muladd $p |- ( ( A + B ) x. ( C + D ) ) =
        ( ( ( A x. C ) + ( D x. B ) ) + ( ( A x. D ) + ( C x. B ) ) ) $=
      ( caddc co cmulc addcl distr1 distr2 opreq12i mulcl add23 mulcom opreq2i
      addass eqtr2 eqtr 3eqtr ) ABIJZCDIJKJUDCKJZUDDKJZIJACKJZBCKJZIJZADKJZBDKJ
      ZIJZIJZUGDBKJZIJZUJCBKJZIJIJZUDCDABEFLGHMUEUIUFULIABCEFGNABDEFHNOUMUGULIJ
      ZUHIJUOUJIJZUPIJUQUGUHULACEGPZBCFGPUJUKADEHPZBDFHPZLQURUSUHUPIURUGUJIJZUN
      IJZUSVDVCUKIJURUNUKVCIDBHFRSUGUJUKUTVAVBTUAUGUJUNUTVADBHFPZQUBBCFGROUOUJU
      PUGUNUTVELVACBGFPTUCUC $.
      $([17-May-99]$)
  $}

  ${
    subdist.1 $e |- A e. CC $.
    subdist.2 $e |- B e. CC $.
    subdist.3 $e |- C e. CC $.
    $( Distribution of muliplication over subtraction.  Theorem I.5 of
       Apostol, p. 18. $)
    subdist   $p |- ( A x. ( B - C ) ) = ( ( A x. B ) - ( A x. C ) ) $=
      ( cmulc co cmin wceq caddc mulcl subcl subadd distr1 subaddeq opreq2i
      eqtr3 mpbir cleqcomi ) ABGHZACGHZIHZABCIHZGHZUCUEJUBUEKHZUAJUAUBUEABDELAC
      DFLAUDDBCEFMZLNACUDKHZGHUFUAACUDDFUGOUHBAGCBFEPQRST $.
      $([26-Nov-94]$)

    $( Distribution of muliplication over subtraction.  Theorem I.5 of
       Apostol, p. 18. $)
    subdistr   $p |- ( ( A - B ) x. C ) = ( ( A x. C ) - ( B x. C ) ) $=
      ( cmin co cmulc subdist subcl mulcom opreq12i 3eqtr4 ) CABGHZIHCAIHZCBIHZ
      GHOCIHACIHZBCIHZGHCABFDEJOCABDEKFLRPSQGACDFLBCEFLMN $.
      $([8-May-99]$)
  $}

  ${
    mulzer.1 $e |- A e. CC $.

    $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
    mulzer1  $p |- ( A x. 0 _10 ) = 0 _10 $=
      ( cn0 cmin co cmulc 0cn subdist subid opreq2i mulcl 3eqtr3 ) ACCDEZFEACFE
      ZNDENCACCBGGHMCAFCGIJNACBGKIL $.
      $([23-Nov-94]$)

    $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
    mulzer2  $p |- ( 0 _10 x. A ) = 0 _10 $=
      ( cn0 0cn mulzer1 mulcoml ) ACCBDDABEF $.
      $([23-Nov-94]$)

    $( Two times a number. $)
    1p1times $p |- ( ( 1 _10 + 1 _10 ) x. A ) = ( A + A ) $=
      ( cn1 caddc co cmulc ax1re recn distr2 mulid2 opreq12i eqtr ) CCDEAFECAFE
      ZMDEAADECCACGHZNBIMAMADABJZOKL $.
      $([18-May-99]$)
  $}

  $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
  mulzer1t $p |- ( A e. CC -> ( A x. 0 _10 ) = 0 _10 ) $=
    ( cc wcel cn0 cmulc co wceq cded opreq1 cleq1d 0cn elimel mulzer1 dedth )
    ABCZADEFZDGOADHZDEFZDGADAQGPRDAQDEIJQADBKLMN $.
    $([15-May-99]$)

  $( Multiplication by 0.  Theorem I.6 of Apostol, p. 18. $)
  mulzer2t $p |- ( A e. CC -> ( 0 _10 x. A ) = 0 _10 ) $=
    ( cc wcel cn0 cmulc co wceq axmulcom 0cn mpan mulzer1t eqtrd ) ABCZDAEFZADE
    FZDDBCMNOGDAHIJAKL $.
    $([10-Aug-99]$)

  ${
    mulneg.1 $e |- A e. CC $.
    ${
      mulneg.2 $e |- B e. CC $.

      $( Product with negative is negative of product.  Theorem I.12 of Apostol,
         p. 18. $)
      mulneg1   $p |- ( -u A x. B ) = -u ( A x. B ) $=
        ( cn0 cmulc co cmin cneg mulzer1 mulcom opreq12i df-neg opreq1i 0cn
        subcl subdist 3eqtr 3eqtr4 ) BEFGZBAFGZHGZEABFGZHGAIZBFGZUCITEUAUCHBDJB
        ADCKLUEEAHGZBFGBUFFGUBUDUFBFAMNUFBEAOCPDKBEADOCQRUCMS $.
        $([10-Feb-95]$)

      mulneg2   $p |- ( A x. -u B ) = -u ( A x. B ) $=
        ( cneg cmulc co negcl mulcom mulneg1 negeqi 3eqtr ) ABEZFGMAFGBAFGZEABF
        GZEAMCBDHIBADCJNOBADCIKL $.
        $([31-Jul-99]$)

      $( Product of two negatives.  Theorem I.12 of Apostol, p. 18. $)
      mul2neg   $p |- ( -u A x. -u B ) = ( A x. B ) $=
        ( cneg cmulc co negcl mulcl mulneg1 mulcoml negeqi negneg 3eqtr
        mulcomr ) BAAEZBEZFGZDCPQACHBDHZIRAQFGZEBAFGZEZEUAAQCSJTUBQAUBSCUABADCI
        ZHBADCJKLUAUCMNO $.
        $([14-Feb-95]$)

      $( Distribution of negative over addition. $)
      negdist $p |- -u ( A + B ) = ( -u A + -u B ) $=
        ( cn1 caddc co cmulc cneg addcl mulid2 negeqi 1cn mulneg1 negcl distr1
        ax1re recn eqtr opreq12i eqtr3 ) EABFGZHGZIZUBIAIZBIZFGZUCUBUBABCDJZKLE
        IZUBHGZUDUGEUBMUHNUJUIAHGZUIBHGZFGUGUIABEMOCDPUKUEULUFFUKEAHGZIUEEAEQRZ
        CNUMAACKLSULEBHGZIUFEBUNDNUOBBDKLSTSUAUA $.
        $([28-Jul-99]$)

      $( Distribution of negative over subtraction. $)
      negdist2 $p |- -u ( A - B ) = ( -u A + B ) $=
        ( cmin co cneg caddc subneg negeqi negcl negdist negneg opreq2i 3eqtr
        ) ABEFZGABGZHFZGAGZQGZHFSBHFPRABCDIJAQCBDKLTBSHBDMNO $.
        $([6-Aug-99]$)
    $}

    $( Product with minus one is negative. $)
    mulm1 $p |- ( -u 1 _10 x. A ) = -u A $=
      ( cn1 cneg cmulc co 1cn mulneg1 mulid2 negeqi eqtr ) CDAEFCAEFZDADCAGBHLA
      ABIJK $.
      $([31-Jul-99]$)
  $}

  $( Product with negative is negative of product.  Theorem I.12 of Apostol,
     p. 18. $)
  mulnegt $p |- ( ( A e. CC /\ B e. CC ) ->
                ( -u A x. B ) = -u ( A x. B ) ) $=
    ( cc wcel cneg cmulc co wceq cn0 cded negeq opreq1d opreq1 negeqd cleq12d
    opreq2 0cn elimel mulneg1 dedth2h ) ACDZBCDZAEZBFGZABFGZEZHUAAIJZEZBFGZUGBF
    GZEZHUHUBBIJZFGZUGULFGZEZHABIIAUGHZUDUIUFUKUPUCUHBFAUGKLUPUEUJAUGBFMNOBULHZ
    UIUMUKUOBULUHFPUQUJUNBULUGFPNOUGULAICQRBICQRST $.
    $([14-May-99]$)

  ${
    sub4.1 $e |- A e. CC $.
    sub4.2 $e |- B e. CC $.
    sub4.3 $e |- C e. CC $.
    sub4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms in a mixed addition and subtraction. $)
    sub4   $p |- ( ( A - B ) + ( C - D ) ) =
                 ( ( A + C ) - ( B + D ) ) $=
      ( cneg caddc co cmin negcl add4 negdist opreq2i eqtr4 subneg opreq12i
      addcl 3eqtr4 ) ABIZJKZCDIZJKZJKZACJKZBDJKZIZJKZABLKZCDLKZJKUGUHLKUFUGUBUD
      JKZJKUJAUBCUDEBFMGDHMNUIUMUGJBDFHOPQUKUCULUEJABEFRCDGHRSUGUHACEGTBDFHTRUA
      $.
      $([28-Jul-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    mulcan.1 $e |- A e. CC $.
    mulcan.2 $e |- B e. CC $.
    mulcan.3 $e |- C e. CC $.
    mulcan.4 $e |- A =/= 0 _10 $.
    $( Cancellation law for multiplication.  Theorem I.7 of Apostol, p. 18. $)
    mulcan   $p |- ( ( A x. B ) = ( A x. C ) -> B = C ) $=
      ( vx cv cmulc co cn1 wceq cc wrex wi recex wcel wa wb axmulass mp3an3
      cleq12d mpan2 opreq2 syl5bir adantr axmulcom mpan cleq1d opreq1 mulid2
      syl6eq syl6bi imp sylibd exp r19.23aiv ax-mp ) AHIZJKZLMZHNOABJKZACJKZMZB
      CMZPZHADGQVBVGHNUTNRZVBVGVHVBSVEUTAJKZBJKZVICJKZMZVFVHVEVLPVBVHVLUTVCJKZU
      TVDJKZMZVEVHANRZVLVOTVHVPSVJVMVKVNVHVPBNRVJVMMUTABUAEUBVHVPCNRVKVNMUTACUA
      FUBUCDUDVCVDUTJUEUFUGVHVBVLVFTZVHVBVILMZVQVHVAVILVPVHVAVIMAUTUHDUIUJVRVJB
      VKCVRVJLBJKBVILBJUKBEULUMVRVKLCJKCVILCJUKCFULUMUCUNUOUPUQURUS $.
      $([26-Jan-95]$)
  $}

  ${
    mulcant.1 $e |- A =/= 0 _10 $.
    $( Cancellation law for multiplication (theorem form).  Theorem I.7
       of Apostol, p. 18.  Illustrates use of ~keephyp . $)
    mulcant   $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A x. B ) = ( A x. C ) -> B = C ) ) $=
      ( cc wcel cmulc co wceq wi cn1 cded opreq1 cleq12d birimd opreq2 cleq1d
      cleq1 bi2imd cleq2d cleq2 1cn elimel cn0 wne neeq1 ax1ne0 keephyp mulcan
      dedth3h ) AEFZBEFZCEFZABGHZACGHZIZBCIZJUKAKLZBGHZURCGHZIZUQJABCKKKURULBKL
      ZGHZUTIZVBCIZJVCURUMCKLZGHZIZVBVFIZJAURIZUPVAUQVJUNUSUOUTAURBGMAURCGMNOBV
      BIZVAVDUQVEVKUSVCUTBVBURGPQBVBCRSCVFIZVDVHVEVIVLUTVGVCCVFURGPTCVFVBUASURV
      BVFAKEUBUCBKEUBUCCKEUBUCUKAUDUEKUDUEURUDUEAKAURUDUFKURUDUFDUGUHUIUJ $.
      $([29-Jan-95]$)
  $}

  $( Cancellation law for multiplication (full theorem form).  Theorem I.7
     of Apostol, p. 18.  Illustrates use of ~dedth and ~elimne0 . $)
  mulcant2   $p |- ( ( A =/= 0 _10 /\ ( A e. CC /\ B e. CC /\ C e. CC ) ) ->
                 ( ( A x. B ) = ( A x. C ) -> B = C ) ) $=
    ( cn0 wne cc wcel w3a cmulc co wceq wi cn1 cded eleq1 pm4.2i bi3and opreq1
    cleq12d birimd bi2imd elimne0 mulcant dedth imp ) ADEZAFGZBFGZCFGZHZABIJZAC
    IJZKZBCKZLZUFUJUOLUFAMNZFGZUHUIHZUPBIJZUPCIJZKZUNLZLAMAUPKZUJURUOVBVCUGUQUH
    UHUIUIAUPFOVCUHPVCUIPQVCUMVAUNVCUKUSULUTAUPBIRAUPCIRSTUAUPBCAUBUCUDUE $.
    $([29-Jan-95]$)

  ${
    mul0or.1 $e |- A e. CC $.
    ${
      mul0or.2 $e |- B e. CC $.
      $( If a product is zero, one of its factors must be zero.  Theorem I.11
         of Apostol, p. 18. $)
      mul0or   $p |- ( ( A x. B ) = 0 _10 -> ( A = 0 _10 \/ B = 0 _10 ) ) $=
        ( cmulc co cn0 wceq wn wne wi df-ne cc wcel w3a mulcant2 0cn 3pm3.2i
        mpan2 mulzer1 cleq2i syl5ibr sylbir comm orrd ) ABEFZGHZAGHZBGHZUHIZUGU
        IUJAGJZUGUIKAGLUKUFAGEFZHZUIUGUKAMNZBMNZGMNZOUMUIKABGPUNUOUPCDQRSULGUFA
        CTUAUBUCUDUE $.
        $([10-Feb-95]$)

      ${
        muln0.3 $e |- A =/= 0 _10 $.
        muln0.4 $e |- B =/= 0 _10 $.
        $( The product of two non-zero numbers is non-zero. $)
        muln0   $p |- ( A x. B ) =/= 0 _10 $=
          ( cn0 wne cmulc co wceq wn wi df-ne wa wo ioran mul0or con3 sylbir
          exp sylbi 3imtr4g mp2 ) AGHZBGHZABIJZGHZUEBGKZLZUGGKZLZUFUHUEAGKZLZUJ
          ULMAGNUNUJULUNUJOUMUIPZLULUMUIQUKUOABCDRSTUAUBBGNUGGNUCEFUD $.
          $([15-Feb-95]$)
      $}
    $}

    $( A number is zero iff its square is zero. $)
    sq0 $p |- ( ( A x. A ) = 0 _10 <-> A = 0 _10 ) $=
      ( cmulc co cn0 wceq wo mul0or orabs sylibr opreq2 mulzer1 syl6eq impbi )
      AACDZEFZAEFZPQQGQAABBHQIJQOAECDEAEACKABLMN $.
      $([28-Jul-99]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` y ` is dummy. $)
    receux.1 $e |- A e. CC $.
    receux.2 $e |- B e. CC $.
    receux.3 $e |- A =/= 0 _10 $.
    $( Existential uniqueness of reciprocals.  Theorem I.8 of Apostol,
       p. 18. $)
    receux   $p |- E! x e. CC ( A x. x ) = B $=
      ( vy cv cmulc co wceq cc wreu wrex wa weq wi wral opreq2 cleq1d reu4 cn1
      recex wcel axmulass mp3an1 mpan2 cleqcomd sylan9eqr opreq1 mulid2 syl6eq
      exp32 comm imp a1i r19.21aiv exp r19.22 syl6 com23 axmulcl risset sylib
      mpd r19.23aiv ax-mp w3a mulcant cleq2 biimparc syl5 rgen2 pm3.2i mpbir )
      BAHZIJZCKZALMVRALNZVRBGHZIJZCKZOZAGPZQZGLRALRZOVRWBAGLWDVQWACVPVTBISTUAVS
      WFWAUBKZGLNVSGBDFUCWGVSGLVTLUDZVPVTCIJZKZALNZWGVSQWHWGWKVSWHWGWJVRQZALRZW
      KVSQWHWGWMWHWGOZWLALWNWLQVPLUDZWHWGWLWGWHWLWGWHWJVRWHWJOWGVQWACIJZCWJWHVQ
      BWIIJZWPVPWIBISWHWPWQWHCLUDZWPWQKZBLUDZWHWRWSBVTCUEDUFEUGUHUIWGWPUBCIJCWA
      UBCIUJCEUKULUIUMUNUOUPUQURWJVRALUSUTVAWHWILUDZWKWHWRXAVTCVBEUGAWILVCVDVEV
      FVGWEAGLWTWOWHWEWTWOWHVHVQWAKZWDWCBVPVTFVIWBXBVRWACVQVJVKVLDUFVMVNVO $.
      $([29-Jan-95]$)
  $}


