$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some deductions from the field axioms for complex numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  recnt $p |- ( A e. RR -> A e. CC ) $=
    ( cr cc axresscn ssi ) BCADE $.
    $([10-Aug-99]$)

  ${
    recn.1 $e |- A e. RR $.
    $( A real number is a complex number. $)
    recn   $p |- A e. CC $=
      ( cr cc axresscn ssii ) CDAEBF $.
      $([1-Mar-95]$)
  $}

  $( 0 is a complex number. $)
  0cn $p |- 0 _10 e. CC $=
    ( cn0 ax0re recn ) ABC $.
    $([1-Mar-95]$)

  $( 1 is a complex number. $)
  1cn $p |- 1 _10 e. CC $=
    ( cn1 ax1re recn ) ABC $.
    $([1-Mar-95]$)

  $( Hypothesis for deduction theorem to eliminate ` A =/= 0 _10 ` . $)
    elimne0 $p |- ded ( A =/= 0 _10 , A , 1 _10 ) =/= 0 _10 $=
      ( cn0 wne cn1 cded neeq1 ax1ne0 elimhyp ) ABCZIADEZBCDBCADAJBFDJBFGH $.
      $([15-May-99]$)

  ${
    axi.1 $e |- A e. CC $.
    ${
      axi.2 $e |- B e. CC $.

      $( Closure law for addition. $)
      addcl $p |- ( A + B ) e. CC $=
        ( cc wcel caddc co axaddcl mp2an ) AEFBEFABGHEFABICDJ $.
        $([23-Nov-94]$)

      $( Closure law for multiplication. $)
      mulcl $p |- ( A x. B ) e. CC $=
        ( cc wcel cmulc co axmulcl mp2an ) AEFBEFABGHEFABICDJ $.
        $([23-Nov-94]$)

      $( Commutative law for addition. $)
      addcom $p |- ( A + B ) = ( B + A ) $=
        ( cc wcel caddc co wceq axaddcom mp2an ) AEFBEFABGHBAGHIABJCDK $.
        $([23-Nov-94]$)

      $( Commutative law for multiplication. $)
      mulcom $p |- ( A x. B ) = ( B x. A ) $=
        ( cc wcel cmulc co wceq axmulcom mp2an ) AEFBEFABGHBAGHIABJCDK $.
        $([23-Nov-94]$)

      ${
        axi.3 $e |- C e. CC $.

        $( Associative law for addition. $)
        addass $p |- ( ( A + B ) + C ) = ( A + ( B + C ) ) $=
          ( cc wcel caddc co wceq axaddass mp3an ) AGHBGHCGHABIJCIJABCIJIJKABCL
          DEFM $.
          $([23-Nov-94]$)

        $( Associative law for multiplication. $)
        mulass $p |- ( ( A x. B ) x. C ) = ( A x. ( B x. C ) ) $=
          ( cc wcel cmulc co wceq axmulass mp3an ) AGHBGHCGHABIJCIJABCIJIJKABCL
          DEFM $.
          $([23-Nov-94]$)

        $( Distributive law. $)
        distr1 $p |- ( A x. ( B + C ) ) = ( ( A x. B ) + ( A x. C ) ) $=
          ( cc wcel caddc co cmulc wceq axdistr mp3an ) AGHBGHCGHABCIJKJABKJACK
          JIJLABCMDEFN $.
          $([23-Nov-94]$)

        $( Distributive law. $)
        distr2 $p |- ( ( A + B ) x. C ) = ( ( A x. C ) + ( B x. C ) ) $=
          ( caddc co cmulc distr1 addcl mulcom opreq12i 3eqtr4 ) CABGHZIHCAIHZC
          BIHZGHOCIHACIHZBCIHZGHCABFDEJOCABDEKFLRPSQGACDFLBCEFLMN $.
          $([16-Feb-95]$)

        ${
          addcoml.4 $e |- ( A + B ) = C $.
          $( Left commutative deduction for addition. $)
          addcoml   $p |- ( B + A ) = C $=
            ( caddc co addcom eqtr ) BAHIABHICBAEDJGK $.
            $([23-Nov-94]$)
        $}

        ${
          addcomr.4 $e |- C = ( A + B ) $.
          $( Right commutative deduction for addition. $)
          addcomr   $p |- C = ( B + A ) $=
            ( caddc co addcom eqtr ) CABHIBAHIGABDEJK $.
            $([23-Nov-94]$)
        $}

        ${
          mulcoml.4 $e |- ( A x. B ) = C $.
          $( Left commutative deduction for multiplication. $)
          mulcoml   $p |- ( B x. A ) = C $=
            ( cmulc co mulcom eqtr ) BAHIABHICBAEDJGK $.
            $([23-Nov-94]$)
        $}

        ${
          mulcomr.4 $e |- C = ( A x. B ) $.
          $( Right commutative deduction for multiplication. $)
          mulcomr   $p |- C = ( B x. A ) $=
            ( cmulc co mulcom eqtr ) CABHIBAHIGABDEJK $.
            $([23-Nov-94]$)
        $}

        ${
          axi.4 $e |- D e. CC $.

          ${
            addass1l.5 $e |- ( A + ( B + C ) ) = D $.
            $( Left associative deduction for addition. $)
            addass1l   $p |- ( ( A + B ) + C ) = D $=
              ( caddc co addass eqtr ) ABJKCJKABCJKJKDABCEFGLIM $.
              $([23-Nov-94]$)
          $}

          ${
            addass2l.5 $e |- ( ( A + B ) + C ) = D $.
            $( Left associative deduction for addition. $)
            addass2l   $p |- ( A + ( B + C ) ) = D $=
              ( caddc co addass eqtr3 ) ABJKCJKABCJKJKDABCEFGLIM $.
              $([23-Nov-94]$)
          $}

          ${
            addass1r.5 $e |- D = ( A + ( B + C ) ) $.
            $( Right associative deduction for addition. $)
            addass1r   $p |- D = ( ( A + B ) + C ) $=
              ( caddc co addass eqtr4 ) DABCJKJKABJKCJKIABCEFGLM $.
              $([23-Nov-94]$)
          $}

          ${
            addass2r.5 $e |- D = ( ( A + B ) + C ) $.
            $( Right associative deduction for addition. $)
            addass2r   $p |- D = ( A + ( B + C ) ) $=
              ( caddc co addass eqtr ) DABJKCJKABCJKJKIABCEFGLM $.
              $([23-Nov-94]$)
          $}

          ${
            mulass1l.5 $e |- ( A x. ( B x. C ) ) = D $.
            $( Left associative deduction for multiplication. $)
            mulass1l   $p |- ( ( A x. B ) x. C ) = D $=
              ( cmulc co mulass eqtr ) ABJKCJKABCJKJKDABCEFGLIM $.
              $([9-Feb-95]$)
          $}

          ${
            mulass2l.5 $e |- ( ( A x. B ) x. C ) = D $.
            $( Left associative deduction for multiplication. $)
            mulass2l   $p |- ( A x. ( B x. C ) ) = D $=
              ( cmulc co mulass eqtr3 ) ABJKCJKABCJKJKDABCEFGLIM $.
              $([9-Feb-95]$)
          $}

          ${
            mulass1r.5 $e |- D = ( A x. ( B x. C ) ) $.
            $( Right associative deduction for multiplication. $)
            mulass1r   $p |- D = ( ( A x. B ) x. C ) $=
              ( cmulc co mulass eqtr4 ) DABCJKJKABJKCJKIABCEFGLM $.
              $([9-Feb-95]$)
          $}

          ${
            mulass2r.5 $e |- D = ( ( A x. B ) x. C ) $.
            $( Right associative deduction for multiplication. $)
            mulass2r   $p |- D = ( A x. ( B x. C ) ) $=
              ( cmulc co mulass eqtr ) DABJKCJKABCJKJKIABCEFGLM $.
              $([9-Feb-95]$)
          $}
        $}
      $}
    $}

    $( Identity law for addition. $)
    addid1 $p |- ( A + 0 _10 ) = A $=
      ( cc wcel cn0 caddc co wceq ax0id ax-mp ) ACDAEFGAHBAIJ $.
      $([23-Nov-94]$)

    $( Identity law for addition. $)
    addid2 $p |- ( 0 _10 + A ) = A $=
      ( cn0 0cn addid1 addcoml ) ACABDBABEF $.
      $([23-Nov-94]$)

    $( Identity law for multiplication. $)
    mulid1 $p |- ( A x. 1 _10 ) = A $=
      ( cc wcel cn1 cmulc co wceq ax1id ax-mp ) ACDAEFGAHBAIJ $.
      $([14-Feb-95]$)

    $( Identity law for multiplication. $)
    mulid2 $p |- ( 1 _10 x. A ) = A $=
      ( cn1 1cn mulid1 mulcoml ) ACABDBABEF $.
      $([14-Feb-95]$)

    ${
      $d x A $.
      $( Existence of negatives. $)
      negex $p |- E. x e. CC ( A + x ) = 0 _10 $=
        ( cc wcel cv caddc co cn0 wceq wrex axnegex ax-mp ) BDEBAFGHIJADKCABLM
        $.
        $([23-Nov-94]$)

      ${
        recex.2 $e |- A =/= 0 _10 $.
        $( Existence of reciprocals. $)
        recex $p |- E. x e. CC ( A x. x ) = 1 _10 $=
          ( cc wcel cn0 wne cv cmulc co cn1 wceq wrex axrecex mp2 ) BEFBGHBAIJK
          LMAENABOCDP $.
          $([26-Jan-95]$)
      $}
    $}
  $}

  ${
    axri.1 $e |- A e. RR $.
    axri.2 $e |- B e. RR $.

    $( Closure law for addition of reals. $)
    addrcl $p |- ( A + B ) e. RR $=
      ( cr wcel caddc co axaddrcl mp2an ) AEFBEFABGHEFABICDJ $.
      $([17-Jan-97]$)

    $( Closure law for multiplication of reals. $)
    mulrcl $p |- ( A x. B ) e. RR $=
      ( cr wcel cmulc co axmulrcl mp2an ) AEFBEFABGHEFABICDJ $.
      $([17-Jan-97]$)
  $}

