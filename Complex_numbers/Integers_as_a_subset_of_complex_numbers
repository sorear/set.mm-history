$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of integers.  Definition (set of integers) of [Apostol]
     p. 22. $)
  df-z $a |- ZZ = { x e. RR | ( x = 0 \/ x e. NN \/ -u x e. NN ) } $.

  ${
    $d x A $.
    $( Membership in the set of integers. $)
    elz $p |- ( A e. ZZ <->
              ( A e. RR /\ ( A = 0 \/ A e. NN \/ -u A e. NN ) ) ) $=
      ( vx cz wcel cv cc0 wceq cn cneg w3o cr crab wa df-z eleq2i cleq1 eleq1
      negeq eleq1d bi3ord elrab bitr ) ACDABEZFGZUCHDZUCIZHDZJZBKLZDAKDAFGZAHDZ
      AIZHDZJZMCUIABNOUHUNBAKUCAGZUDUJUEUKUGUMUCAFPUCAHQUOUFULHUCARSTUAUB $.
      $([8-Jan-02]$)
  $}

  $( Zero is an integer. $)
  0z $p |- 0 e. ZZ $=
    ( cc0 cz wcel cr wceq cn cneg w3o wa ax0re wo cleqid orc ax-mp 3orass
    mpbir pm3.2i elz ) ABCADCZAAEZAFCZAGFCZHZISUCJUCTUAUBKZKZTUEALTUDMNTUAUBOPQ
    ARP $.
    $([12-Jan-02]$)

  $( The negative of a natural number is an integer. $)
  nnnz $p |- ( A e. NN -> -u A e. ZZ ) $=
    ( cn wcel cneg cr cc0 wceq w3o wa cz nnret negrclt syl wo cc nncnt negnegt
    eleq1d biimprd mpcom olc df-3or sylibr jca elz ) ABCZADZECZUGFGZUGBCZUGDZBC
    ZHZIUGJCUFUHUMUFAECUHAKALMUFUIUJNZULNZUMUFULUOAOCZUFULAPUPULUFUPUKABAQRSTUL
    UNUAMUIUJULUBUCUDUGUEUC $.
    $([12-Jan-02]$)

  $( An integer is a real. $)
  zr $p |- ( A e. ZZ -> A e. RR ) $=
    ( cz wcel cr cc0 wceq cn cneg w3o elz pm3.26bd ) ABCADCAEFAGCAHGCIAJK $.
    $([8-Jan-02]$)

  $( Natural number expressed in terms of integers. $)
  eln $p |- ( A e. NN <-> ( A e. ZZ /\ 0 < A ) ) $=
    ( cn wcel cr cneg cc0 wceq wo wa clt wbr cz nnret orc jca nngt0t wi idd wn
    wb ax0re ltnegt mpan negrclt negrcl ltnsymt mpan2 syl sylbid imp neg0
    breq1i bineg sylib nsyl ltnet cleqcom sylibr ioran pm2.21d jaod exp com23
    imp31 impbi w3o elz 3orrot 3orass bitr bilan biran bitr4 ) ABCZADCZVNAEZBCZ
    AFGZHZHZIZFAJKZIZALCZWBIVNWCVNWAWBVNVOVTAMVNVSNOAPOVOVTWBVNVOWBVTVNVOWBVTVN
    QVOWBIZVNVNVSWEVNRWEVSVNWEVQSZVRSZIVSSWEWFWGWEFVPJKZVQWEFEZVPJKZSZWHSVOWBWK
    VOWBVPWIJKZWKFDCZVOWBWLTUAFAUBUCVOVPDCZWLWKQZAUDWNWIDCWOFUAUEVPWIUFUGUHUIUJ
    WJWHWIFVPJUKULUMUNVPPUOWEFAGZSZWGVOWBWQWMVOWBWQQUAFAUPUCUJVRWPAFUQUMUROVQVR
    USURUTVAVBVCVDVEWDWAWBWDVOVRVNVQVFZIWAAVGWRVTVOWRVNVQVRVFVTVRVNVQVHVNVQVRVI
    VJVKVJVLVM $.
    $([8-Jan-02]$)

  $( Natural numbers are a subset of integers. $)
  nnssz $p |- NN (_ ZZ $=
    ( vx cn cz cv wcel cc0 clt wbr eln pm3.26bd ssriv ) ABCADZBELCEFLGHLIJK $.
    $([9-Jan-02]$)

  $( Nonnegative integers are a subset of the integers. $)
  nn0ssz $p |- NN0 (_ ZZ $=
    ( cn0 cn cc0 csn cun cz df-n0 nnssz wcel wss 0z elisseti snss mpbi unssi
    eqsstr ) ABCDZEFGBQFHCFIQFJKCFCFKLMNOP $.
    $([?]$)

