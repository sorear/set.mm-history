$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Our natural numbers include zero (some authors don't).  They are
       defined as the smallest inductive set containing zero. $)
    df-n  $a |- NN =
       |^| { x | ( 0 _10 e. x /\ A. y ( y e. x -> ( y + 1 _10 ) e. x ) ) } $.
  $}

  $( Define the set of integers. $)
  df-z $a |- ZZ = { x | ( x e. NN \/ ( - x ) e. NN ) } $.

  ${
    $d x y z $.
    $( Define the set of rationals. $)
    df-q $a |- QQ =
                { x | E. y e. ZZ E. z e. ( ZZ \ { 0 _10 } ) x = ( y / z ) } $.
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    peano5c.1 $e |- A e. V $.
    $( Peano's inductive postulate. $)
    peano5c   $p |- ( ( 0 _10 e. A /\
                 A. x ( x e. A -> ( x + 1 _10 ) e. A ) ) -> NN (_ A ) $=
      ( vy cn0 wcel cv cn1 caddc co wi wal wa wel cab cint wss cn wceq cleq4
      bi2imd bialdv bi2and abcl intss sylbir df-n sseq1i sylibr ) EBFZAGZBF
      ZUKHIJZBFZKZALZMZEDGZFZADNZUMURFZKZALZMZDOZPZBQZRBQUQBVEFVGVDUQDBCURBSZUS
      UJVCUPURBETVHVBUOAVHUTULVAUNURBUKTURBUMTUAUBUCUDBVEUEUFRVFBDAUGUHUI $.
      $([10-Jan-97]$)
  $}

  $( ` x ` is dummy. $)
  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN (_ RR $=
      ( vx cn0 cr wcel cv cn1 caddc co wi wal wa cn wss cc axcnex axresscn
      ssexi
      peano5c ax0re axaddrcl ax1re mpan2 ax-gen pm3.2i ax-mp ) BCDZAEZCDZUGFGHC
      DZIZAJZKLCMACCNOPQRUFUKSUJAUHFCDUIUGFTUAUBUCUDUE $.
      $([10-Jan-97]$)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Peano postulate: 0 is a natural number. $)
    peano1c $p |- 0 _10 e. NN $=
      ( vx vy cn0 cn wcel cv wel cn1 caddc co wi wal wa cab cint df-n cleq4i
      cc 0cn elisseti inteqab pm3.26 mpgbir mpbir ) CDECCAFZEZBAGBFHIJUEEKBLZMZ
      ANOZEZDUICABPQUJUHUFKAUHACCRSTUAUFUGUBUCUD $.
      $([11-Jan-97]$)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2c $p |- ( A e. NN -> ( A + 1 _10 ) e. NN ) $=
      ( vz vx vy cv cn1 caddc co cn wcel wceq opreq1 cleq3d cn0 wel wi wal wa
      cab cint weq a13b bi2imd a4b1 adantl a2i 19.20i visset inteqab oprex
      3imtr4 df-n cleq4i vtoclga ) BEZFGHZIJZAFGHZIJBAIUOAKUPURIUOAFGLMUONCEZJ
      ZDCOZDEZFGHZUSJZPZDQZRZCSTZJZUPVHJZUOIJUQVGBCOZPZCQVGUPUSJZPZCQVIVJVLVNCV
      GVKVMVFVKVMPZUTVEVODBDBUAZVAVKVDVMDBCUBVPVCUPUSVBUOFGLMUCUDUEUFUGVGCUOBUH
      UIVGCUPUOFGUJUIUKIVHUOCDULZUMIVHUPVQUMUKUN $.
      $([11-Jan-97]$)

  $}

  $( 1 is a natural number. $)
  1cnn $p |- 1 _10 e. NN $=
      ( cn0 cn1 caddc co cn wcel 1cn addid2 cleq3i peano2c peano1c ax-mp mpbi
      ) ABCDZEFZBEFNBEBGHIAEFOAJKLM $.
      $([12-Jan-97]$)

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    ind.1 $e |- ( x = 0 _10 -> ( ph <-> ps ) ) $.
    ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    ind.3 $e |- ( x = ( y + 1 _10 ) -> ( ph <-> th ) ) $.
    ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    ind.5 $e |- ps $.
    $( Induction hypothesis. $)
    ind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema). $)
    ind $p |- ( A e. NN -> ta ) $=
      ( cn wcel cv wa cab wceq cleq3 bi2and abclg cn0 cn1 caddc co wi wal wss
      cr cc axcnex axresscn ssexi nnssre zfausab peano5c 0cn elisseti abcl
      peano1c pm3.2i mpbir abid2 cleq4i peano2c a1i im2and visset weq oprex
      3imtr4g sylbir pm3.26 ssab2i ssi mpcom ax-gen mp2an mpbid pm3.27d ) HOP
      ZWCEWCHFQZOPZARZFSZPWCERZWFWHFHOWDHTWEWCAEWDHOUALUBUCOWGHUDWGPZGQZWGPZWJU
      EUFUGZWGPZUHZGUIOWGUJGWGAFOOUKUKULUMUNUOUPUOUQURWIUDOPZBRZWFWPFUDUDULUSUT
      WDUDTWEWOABWDUDOUAIUBVAWOBVBMVCVDWNGWJWEFSZPZWKWMWRWJOPZWNOWQWJFOVEVFWSWS
      CRZWLOPZDRZWKWMWSWSXACDWSXAUHWSWJVGVHNVIWFWTFWJGVJFGVKWEWSACWDWJOUAJUBVAW
      FXBFWLWJUEUFVLWDWLTWEXAADWDWLOUAKUBVAVMVNWGWQWJWFWEFWEAVOVPVQVRVSVTVQWAWB
      $.
      $([10-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers. $)
    nnaddcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi cn0 cn1 wceq opreq2 cleq3d bilimd weq cc
      ax0id biimprd cr wss sstr nnssre axresscn mp2 ssi mpcom wa axaddass 1cn
      mp3an3 syl2an peano2c syl5bi exp comm a2d ind imp ) AEFZBEFZABGHZEFZVBVAV
      DVAACIZGHZEFZJVAAKGHZEFZJVAADIZGHZEFZJVAAVJLGHZGHZEFZJVAVDJCDBVEKMZVGVIVA
      VPVFVHEVEKAGNOPCDQZVGVLVAVQVFVKEVEVJAGNOPVEVMMZVGVOVAVRVFVNEVEVMAGNOPVEBM
      ZVGVDVAVSVFVCEVEBAGNOPARFZVAVIVTVIVAVTVHAEASOTERAEUAUBUARUBERUBEUARUCUDUE
      UFZUGZUHVJEFZVAVLVOVAWCVLVOJZVAWCWDVAWCUIZVKLGHZEFVOVLWEWFVNEVTVJRFZWFVNM
      ZVAWCVTWGLRFWHAVJLUJUKULWBERVJWAUGUMOVKUNUOUPUQURUSUQUT $.
      $([12-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmulcl $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmulc co cv wi cn0 cn1 caddc wceq opreq2 cleq3d bilimd
      weq cc cr wss sstr nnssre axresscn mp2 ssi wa cab opreq1 cleq1d 0cn
      cldedg2 mulzer1 cldedg3 syl peano1c mpbiri axdistr 1cn mp3an3 ax1id
      opreq2d adantr eqtrd syl2an nnaddcl ancoms syl5bir exp4b pm2.43b a2d ind
      imp ) BEFZAEFZABGHZEFZVNVOVQVOACIZGHZEFZJVOAKGHZEFZJVOADIZGHZEFZJVOAWCLMH
      ZGHZEFZJVOVQJCDBVRKNZVTWBVOWIVSWAEVRKAGOPQCDRZVTWEVOWJVSWDEVRWCAGOPQVRWFN
      ZVTWHVOWKVSWGEVRWFAGOPQVRBNZVTVQVOWLVSVPEVRBAGOPQVOWBKEFVOWAKEVOASFZWAKNZ
      ESAETUATSUAESUAETSUBUCUDUEZUFZWNVRKFWMJVRAFWMUGJCUHZKGHZKNCASKAWQNWAWRKAW
      QKGUIUJWQCASKUKULUMUNUOPUPUQWCEFZVOWEWHWSVOWEWHJVOWSVOWEWHVOWSUGZWHWDAMHZ
      EFZVOWEUGWTWGXAEWMWCSFZWGXANVOWSWMXCUGWGWDALGHZMHZXAWMXCLSFWGXENAWCLURUSU
      TWMXEXANXCWMXDAWDMAVAVBVCVDWPESWCWOUFVEPWEVOXBWDAVFVGVHVIVJVKVLVMVG $.
      $([12-Jan-97]$)
  $}

