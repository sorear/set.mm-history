$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The natural numbers of analysis start at one (unlike the natural
       numbers of set theory, i.e. the members of set omega, which start at
       zero).  This is the convention used by most analysis books, and it
       also convenient for proofs in that we never have to worry about
       division by zero.  See ~ ind for the principle of mathematical
       induction.  Definition (positive integers) of [Apostol] p. 22. $)
    df-n  $a |- NN =
       |^| { x | ( 1 e. x /\ A. y ( y e. x -> ( y + 1 ) e. x ) ) } $.
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    peano5c.1 $e |- A e. V $.
    $( Peano's inductive postulate.  Theorem I.36 (principle of mathematical
       induction) of [Apostol] p. 34. $)
    peano5c   $p |- ( ( 1 e. A /\
                 A. x ( x e. A -> ( x + 1 ) e. A ) ) -> NN (_ A ) $=
      ( vy c1 wcel cv caddc co wi wal wa wel cab cint wss cn wceq eleq2 bi2imd
      bialdv bi2and elab intss sylbir df-n sseq1i sylibr ) EBFZAGZBFZUJEHIZBFZJ
      ZAKZLZEDGZFZADMZULUQFZJZAKZLZDNZOZBPZQBPUPBVDFVFVCUPDBCUQBRZURUIVBUOUQBES
      VGVAUNAVGUSUKUTUMUQBUJSUQBULSTUAUBUCBVDUDUEQVEBDAUFUGUH $.
      $([10-Jan-97]$)
  $}

  $( ` x ` is dummy. $)
  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN (_ RR $=
    ( vx c1 cr wcel cv caddc co wi wal cn wss ax1re axaddrcl mpan2 ax-gen cc
    axcnex axresscn ssexi peano5c mp2an ) BCDZAEZCDZUCBFGCDZHZAIJCKLUFAUDUBUELU
    CBMNOACCPQRSTUA $.
    $([10-Jan-97]$)

  $( A natural number is a real number. $)
  nnret $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre ssi ) BCADE $.
    $([18-Aug-99]$)

  $( A natural number is a complex number. $)
  nncnt $p |- ( A e. NN -> A e. CC ) $=
    ( cn wcel nnret recnd ) ABCAADE $.
    $([18-Aug-99]$)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnre $p |- A e. RR $=
      ( cn wcel cr nnret ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)

    $( A natural number is a complex number. $)
    nncn $p |- A e. CC $=
      ( cn wcel cc nncnt ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)
  $}

  $( The set of natural numbers exists. $)
  nnex $p |- NN e. V $=
    ( cn cr cc axcnex axresscn ssexi nnssre ) ABBCDEFGF $.
    $([3-Oct-99]$)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Peano postulate: 1 is a natural number. $)
    peano1c $p |- 1 e. NN $=
      ( vx vy c1 cv wcel wel caddc co wi wal wa cab cint cn cc 1cn elisseti
      elintab pm3.26 mpgbir df-n eleqtrr ) CCADZEZBAFBDCGHUCEIBJZKZALMZNCUGEUFU
      DIAUFACCOPQRUDUESTABUAUB $.
      $([11-Jan-97]$)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2c $p |- ( A e. NN -> ( A + 1 ) e. NN ) $=
      ( vz vx vy cv c1 caddc co cn wcel wceq opreq1 eleq1d wel wi wal wa cab
      cint weq a13b bi2imd a4b1 adantl a2i 19.20i visset elintab oprex 3imtr4
      df-n eleq2i vtoclga ) BEZFGHZIJZAFGHZIJBAIUNAKUOUQIUNAFGLMUNFCEZJZDCNZDEZ
      FGHZURJZOZDPZQZCRSZJZUOVGJZUNIJUPVFBCNZOZCPVFUOURJZOZCPVHVIVKVMCVFVJVLVEV
      JVLOZUSVDVNDBDBTZUTVJVCVLDBCUAVOVBUOURVAUNFGLMUBUCUDUEUFVFCUNBUGUHVFCUOUN
      FGUIUHUJIVGUNCDUKZULIVGUOVPULUJUM $.
      $([11-Jan-97]$)

  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    ind.1 $e |- ( x = 1 -> ( ph <-> ps ) ) $.
    ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    ind.3 $e |- ( x = ( y + 1 ) -> ( ph <-> th ) ) $.
    ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    ind.5 $e |- ps $.
    $( Induction hypothesis. $)
    ind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The first
       four hypotheses give us the substitution instances we need; the last
       two are the basis and the induction hypothesis.  See ~ nnaddclt for
       an example of its use. $)
    ind $p |- ( A e. NN -> ta ) $=
      ( cn wcel cv wa cab c1 caddc co wi wal wss peano1c pm3.2i cc 1cn
      elisseti wceq eleq1 bi2and elab mpbir pm3.26 ssab2i ssi abid2 eleq2i
      peano2c a1d im2and visset weq oprex 3imtr4g sylbi mpcom ax-gen nnex
      zfausab peano5c mp2an elabg mpbid pm3.27d ) HOPZVREVRHFQZOPZARZFSZPVRERZO
      WBHTWBPZGQZWBPZWETUAUBZWBPZUCZGUDOWBUEWDTOPZBRZWJBUFMUGWAWKFTTUHUIUJVSTUK
      VTWJABVSTOULIUMUNUOWIGWEVTFSZPZWFWHWBWLWEWAVTFVTAUPUQURWMWEOPZWIWLOWEFOUS
      UTWNWNCRZWGOPZDRZWFWHWNWNWPCDWNWPWNWEVAVBNVCWAWOFWEGVDFGVEVTWNACVSWEOULJU
      MUNWAWQFWGWETUAVFVSWGUKVTWPADVSWGOULKUMUNVGVHVIVJGWBAFOVKVLVMVNURWAWCFHOV
      SHUKVTVRAEVSHOULLUMVOVPVQ $.
      $([10-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi c1 wceq opreq2 eleq1d bilimd weq peano2c
      wa cc 1cn axaddass mp3an3 nncnt syl2an syl5bi exp comm a2d ind imp ) AEFZ
      BEFZABGHZEFZULUKUNUKACIZGHZEFZJUKAKGHZEFZJUKADIZGHZEFZJUKAUTKGHZGHZEFZJUK
      UNJCDBUOKLZUQUSUKVFUPUREUOKAGMNOCDPZUQVBUKVGUPVAEUOUTAGMNOUOVCLZUQVEUKVHU
      PVDEUOVCAGMNOUOBLZUQUNUKVIUPUMEUOBAGMNOAQUTEFZUKVBVEUKVJVBVEJZUKVJVKUKVJR
      ZVAKGHZEFVEVBVLVMVDEASFZUTSFZVMVDLZUKVJVNVOKSFVPTAUTKUAUBAUCUTUCUDNVAQUEU
      FUGUHUIUGUJ $.
      $([12-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmulclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmulc co cv wi c1 caddc wceq opreq2 eleq1d bilimd weq cc
      nncnt ax1id biimprd mpcom wa 1cn axdistr mp3an3 opreq2d adantr eqtrd
      syl2an nnaddclt ancoms syl5bir exp4b pm2.43b a2d ind imp ) BEFZAEFZABGHZE
      FZUSUTVBUTACIZGHZEFZJUTAKGHZEFZJUTADIZGHZEFZJUTAVHKLHZGHZEFZJUTVBJCDBVCKM
      ZVEVGUTVNVDVFEVCKAGNOPCDQZVEVJUTVOVDVIEVCVHAGNOPVCVKMZVEVMUTVPVDVLEVCVKAG
      NOPVCBMZVEVBUTVQVDVAEVCBAGNOPARFZUTVGASZVRVGUTVRVFAEATZOUAUBVHEFZUTVJVMWA
      UTVJVMJUTWAUTVJVMUTWAUCZVMVIALHZEFZUTVJUCWBVLWCEVRVHRFZVLWCMUTWAVRWEUCVLV
      IVFLHZWCVRWEKRFVLWFMUDAVHKUEUFVRWFWCMWEVRVFAVILVTUGUHUIVSVHSUJOVJUTWDVIAU
      KULUMUNUOUPUQURUL $.
      $([12-Jan-97]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number greater than or equal to any two
       others. $)
    nn2get $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cle wbr wo cv wrex cr letrit nnret syl2an wi leidt biantrud
      biimpd syl anc2li wceq breq2 bi2and rcla4ev syl6 adantl biantrurd adantr
      jaod mpd ) BDEZCDEZFZBCGHZCBGHZIZBAJZGHZCURGHZFZADKZBLEZCLEZUQULUMBCMBNZC
      NZOUNUOVBUPUMUOVBPULUMUOUMUOCCGHZFZFVBUMUOVHUMVDUOVHPVFVDUOVHVDVGUOCQRSTU
      AVAVHACDURCUBUSUOUTVGURCBGUCURCCGUCUDUEUFUGULUPVBPUMULUPULBBGHZUPFZFVBULU
      PVJULVCUPVJPVEVCUPVJVCVIUPBQUHSTUAVAVJABDURBUBUSVIUTUPURBBGUCURBCGUCUDUEU
      FUIUJUK $.
      $([18-Aug-99]$)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1t $p |- ( A e. NN -> 1 <_ A ) $=
      ( vx vy c1 cv cle wbr caddc co breq2 ax1re leid cn wcel cr wi nnret cc0
      cc wceq recnt ax0id syl breq2d clt wn lt01 ax0re axltadd mp3an1 mpan mpi
      wa axlttrn mp3an3 axaddrcl mpan2 sylan13 exp3a mpd pm2.16d wb jctil
      leltt 3imtr4d sylbird ind ) DBEZFGDDFGDCEZFGZDVIDHIZFGZDAFGBCAVHDDFJVHVID
      FJVHVKDFJVHADFJDKLVIMNVIONZVJVLPVIQVMVJDVIRHIZFGZVLVMVNVIDFVMVISNVNVITVIU
      AVIUBUCUDVMVNDUEGZUFZVKDUEGZUFZVOVLVMVRVPVMVNVKUEGZVRVPPVMRDUEGZVTUGDONZV
      MWAVTPZKRONZWBVMWCUHRDVIUIUJUKULVMVTVRVPVNONZVKONZVTVRUMVPPZVMWEWFWBWGKVN
      VKDUNUOVMWDWEUHVIRUPUQZVMWBWFKVIDUPUQZURUSUTVAVMWBWEUMVOVQVBVMWEWBWHKVCDV
      NVDUCVMWBWFUMVLVSVBVMWFWBWIKVCDVKVDUCVEVFUCVG $.
      $([25-Aug-99]$)
  $}

  $( A natural number is positive. $)
  nngt0t $p |- ( A e. NN -> 0 < A ) $=
    ( cr wcel c1 cle wbr cc0 clt cn lt01 wa wi ax1re ax0re ltletrt mp3an1 mpan
    mpani nnret nnge1t syl13 ) ABCZDAEFZGAHFZAICUBGDHFZUCUDJDBCZUBUEUCKUDLZMGBC
    UFUBUGNGDAOPQRASATUA $.
    $([26-Sep-99]$)

  $( A number less than one is not a natural number. $)
  lt1nnn $p |- ( ( A e. RR /\ A < 1 ) -> -. A e. NN ) $=
    ( cr wcel c1 clt wbr cn wn cle wb ax1re leltt mpan bicon2d nnge1t con3
    syl6bi imp ) ABCZADEFZAGCZHZSTDAIFZHUBSUCTDBCSUCTHJKDALMNUAUCAOPQR $.
    $([18-Jan-04]$)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0 e. NN $=
    ( cc0 cr wcel c1 clt wbr cn wn ax0re lt01 lt1nnn mp2an ) ABCADEFAGCHIJAKL
    $.
    $([25-Aug-99]$)

  $( A natural number is non-zero. $)
  nnne0t $p |- ( A e. NN -> A =/= 0 ) $=
    ( cn wcel cc0 wceq wn wne 0nnn eleq1 mtbiri con2 df-ne sylibr ) ABCZADEZFAD
    GONONDBCHADBIJKADLM $.
    $([27-Sep-99]$)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive (inference version). $)
    nngt0 $p |- 0 < A $=
      ( cn wcel cc0 clt wbr nngt0t ax-mp ) ACDEAFGBAHI $.
      $([17-Sep-99]$)

    $( A natural number is non-zero (inference version). $)
    nnne0 $p |- A =/= 0 $=
      ( nnre nngt0 gt0ne0i ) AABCABDE $.
      $([25-Aug-99]$)
  $}

  $( The reciprocal of a natural number is positive. $)
  nnrect $p |- ( A e. NN -> 0 < ( 1 / A ) ) $=
    ( cn wcel c1 cle wbr cc0 cdiv co clt nnge1t lt01 cr wa wi nnret ax1re
    ax0re ltletrt mp3an1 mpan recgt0t syld syl mpani mpd ) ABCZDAEFZGDAHIJFZAKU
    GGDJFZUHUILUGAMCZUJUHNZUIOAPUKULGAJFZUIDMCZUKULUMOZQGMCUNUKUORGDASTUAAUBUCU
    DUEUF $.
    $([25-Aug-99]$)

  ${
    $d x y z w v A $.  $d x y z w v B $.
    $( Natural number ordering relation. $)
    nnlelt1 $p |- ( ( A e. NN /\ B e. NN ) ->
                  ( A <_ B <-> A < ( B + 1 ) ) ) $=
      ( vz vx vy vw vv cn wcel wa cle wbr clt wceq wo c1 caddc co cr wb leloet
      nnret syl2an wi cc0 lt01 ax0re ax1re pm3.2i lt2addt an4s mpan2 mpan2i cc
      pm3.26 recnt ax0id 3syl breq1d sylibd breq1 ltplus1t syl5bir comm adantl
      jaod cv wral weq opreq1 breq2d breq2 cleq2 bi2ord bi2imd biraldv cleq1
      wn cleqid a1i orri w3a 3jca ltadd1t syl nnge1t leltt sylan13 mpbid
      pm2.21d sylbird a1d ind rgen bilimd nngt0t 1cn addid2 breq1i sylib
      pm2.15d orrd adantr rcla4v imp adantlr jctir axaddrcl im3an 3anrot
      sylibr addcant bicomd 3simpc 3simp1 jca anandir axaddcom cleqan12d
      bitr4d 3imtr3d exp31 imp3a exp3a com3l r19.21adv cbvralv syl5ib impbid
      bitrd ) AHIZBHIZJZABKLZABMLZABNZOZABPQRZMLZASIZBSIZYNYQTYKYLABUAAUBZBUBZU
      CYMYQYSYTUVAYQYSUDYKYLYTUVAJZYOYSYPUVDYOAUEQRZYRMLZYSUVDYOUEPMLZUVFUFUVDU
      ESIZPSIZJYOUVGJUVFUDZUVHUVIUGUHUIYTUVHUVAUVIUVJAUEBPUJUKULUMUVDUVEAYRMUVD
      YTAUNIUVEANYTUVAUOAUPAUQURUSUTUVAYPYSUDYTYPUVAYSYPYSBYRMLUVAABYRMVABVBVCV
      DVEVFUVBUVCUCYKYLYSYQUDZYLYKUVKYLCVGZYRMLZUVLBMLZUVLBNZOZUDZCHVHZYKUVKUDU
      VLDVGZPQRZMLZUVLUVSMLZCDVIZOZUDZCHVHUVLPPQRZMLZUVLPMLZUVLPNZOZUDZCHVHUVLE
      VGZPQRZMLZUVLUWLMLZCEVIZOZUDZCHVHZUVLUWMPQRZMLZUWNUVLUWMNZOZUDZCHVHZUVRDE
      BUVSPNZUWEUWKCHUXFUWAUWGUWDUWJUXFUVTUWFUVLMUVSPPQVJVKUXFUWBUWHUWCUWIUVSPU
      VLMVLUVSPUVLVMVNVOVPDEVIZUWEUWRCHUXGUWAUWNUWDUWQUXGUVTUWMUVLMUVSUWLPQVJVK
      UXGUWBUWOUWCUWPUVSUWLUVLMVLUVSUWLUVLVMVNVOVPUVSUWMNZUWEUXDCHUXHUWAUXAUWDU
      XCUXHUVTUWTUVLMUVSUWMPQVJVKUXHUWBUWNUWCUXBUVSUWMUVLMVLUVSUWMUVLVMVNVOVPUV
      SBNZUWEUVQCHUXIUWAUVMUWDUVPUXIUVTYRUVLMUVSBPQVJVKUXIUWBUVNUWCUVOUVSBUVLMV
      LUVSBUVLVMVNVOVPUWKCHUVSUWFMLZUVSPMLZUXFOZUDPUWFMLZPPMLZPPNZOZUDUWLUWFMLZ
      UWLPMLZUWLPNZOZUDZUWMUWFMLZUWMPMLZUWMPNZOZUDZUWKDEUVLUXFUXJUXMUXLUXPUVSPU
      WFMVAUXFUXKUXNUXFUXOUVSPPMVAUVSPPVQVNVOUXGUXJUXQUXLUXTUVSUWLUWFMVAUXGUXKU
      XRUXFUXSUVSUWLPMVAUVSUWLPVQVNVOUXHUXJUYBUXLUYEUVSUWMUWFMVAUXHUXKUYCUXFUYD
      UVSUWMPMVAUVSUWMPVQVNVODCVIZUXJUWGUXLUWJUVSUVLUWFMVAUYGUXKUWHUXFUWIUVSUVL
      PMVAUVSUVLPVQVNVOUXPUXMUXNUXOUXOUXNVRPVSVTWAVTUWLHIZUYFUYAUYHUYBUXRUYEUYH
      UWLSIZUVIUVIWBUXRUYBTUYHUYIUVIUVIUWLUBZUVIUYHUHVTZUYKWCUWLPPWDWEUYHUXRUYE
      UYHPUWLKLZUXRVRZUWLWFUVIUYIUYLUYMTUYHPUWLWGUYKUYJWHWIWJWKWLWMWNUYHFVGZUWM
      MLZUYNUWLMLZFEVIZOZUDZFHVHZUXEUWSUYHUYTUXDCHUVLHIZUYHUYTUXDVUAUYHUYTUXDUY
      HUYTJZUVSUWTMLZUVSUWMMLZUXHOZUDZUDVUBPUWTMLZPUWMMLZPUWMNZOZUDZUDVUBGVGZUW
      TMLZVULUWMMLZVULUWMNZOZUDZUDZVUBVULPQRZUWTMLZVUSUWMMLZVUSUWMNZOZUDZUDZVUB
      UXDUDDGUVLUXFVUFVUKVUBUXFVUCVUGVUEVUJUVSPUWTMVAUXFVUDVUHUXHVUIUVSPUWMMVAU
      VSPUWMVQVNVOWODGVIZVUFVUQVUBVVFVUCVUMVUEVUPUVSVULUWTMVAVVFVUDVUNUXHVUOUVS
      VULUWMMVAUVSVULUWMVQVNVOWOUVSVUSNZVUFVVDVUBVVGVUCVUTVUEVVCUVSVUSUWTMVAVVG
      VUDVVAUXHVVBUVSVUSUWMMVAUVSVUSUWMVQVNVOWOUYGVUFUXDVUBUYGVUCUXAVUEUXCUVSUV
      LUWTMVAUYGVUDUWNUXHUXBUVSUVLUWMMVAUVSUVLUWMVQVNVOWOUYHVUKUYTUYHVUJVUGUYHV
      UHVUIUYHVUIVUHUYHVUHVUIVRUYHUEPQRZUWMMLZVUHUYHUEUWLMLZVVIUWLWPUYHUVHUYIUV
      IWBVVJVVITUYHUVHUYIUVIUVHUYHUGVTUYJUYKWCUEUWLPWDWEWIVVHPUWMMPWQWRWSWTWLXA
      XBWLXCVULHIZVVEVURVVKUYHUYTVVDVVKUYHUYTVVDVVKUYHJZUYTJVUNVULUWLMLZGEVIZOZ
      VUTVVCVVKUYTVUNVVOUDZUYHVVKUYTVVPUYTVVKVVPUYSVVPFVULHFGVIZUYOVUNUYRVVOUYN
      VULUWMMVAVVQUYPVVMUYQVVNUYNVULUWLMVAUYNVULUWLVQVNVOXDVDXEXFVVLVUNVUTTZUYT
      VVLVULSIZUWMSIZUVIWBVVRVVLVVSVVTUVIVVKVVSUYHVULUBXCZUYHVVTVVKUYHUYIUVIJVV
      TUYHUYIUVIUYJUHXGUWLPXHWEVEUVIVVLUHVTZWCVULUWMPWDWEXCVVLVVOVVCTZUYTVVLVVS
      UYIUVIWBZVWCVVLVVSUYIUVIVWAUYHUYIVVKUYJVEVWBWCVWDVVMVVAVVNVVBVULUWLPWDVWD
      PUNIZVULUNIZUWLUNIZWBZVVNVVBTVWDVWFVWGVWEWBVWHVVSVWFUYIVWGUVIVWEVULUPUWLU
      PPUPXIVWEVWFVWGXJXKVWHVVNPVULQRZPUWLQRZNZVVBVWHVWKVVNPVULUWLXLXMVWHVWFVWE
      JZVWGVWEJZJZVVBVWKTVWHVWFVWGJZVWEJVWNVWHVWOVWEVWEVWFVWGXNVWEVWFVWGXOXPVWF
      VWGVWEXQWTVWLVWMVUSVWIUWMVWJVULPXRUWLPXRXSWEXTWEVNWEXCYAYBYCWLWMYDYEYFUWR
      UYSCFHCFVIZUWNUYOUWQUYRUVLUYNUWMMVAVWPUWOUYPUWPUYQUVLUYNUWLMVAUVLUYNUWLVQ
      VNVOYGYHWMUVQUVKCAHUVLANZUVMYSUVPYQUVLAYRMVAVWQUVNYOUVOYPUVLABMVAUVLABVQV
      NVOXDWEVDXEYIYJ $.
      $([13-Aug-01]$)
  $}

  $( Natural number ordering relation. $)
  nnltle1 $p |- ( ( A e. NN /\ B e. NN ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel wa clt wbr c1 caddc co cle cr wb ax1re ltadd1t mp3an3 nnret
    syl2an nnlelt1 peano2c sylan bitr4d ) ACDZBCDZEABFGZAHIJZBHIJFGZUFBKGZALDZB
    LDZUEUGMZUCUDUIUJHLDUKNABHOPAQBQRUFCDUDUHUGMUCUFBSATUAUB $.
    $([19-Aug-01]$)

  $( Natural number ordering relation generalized to non-negative integers. $)
  nn0ltle1_ $p |- ( ( A e. ( NN u. { 0 } ) /\ B e. ( NN u. { 0 } ) ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn wcel cc0 wceq wo clt wbr c1 caddc co cle wb csn cun nnltle1 wa breq1
    wi nngt0t nnge1t 1cn addid2 syl5eqbr 2th pm5.74ri sylan9bb opreq1 breq1d
    adantr bitr4d breq2 wn pm5.21 cr nnret ax0re ltnsymt mpan2 syl mt2d
    axlttrn mp3an1 ax1re axaddrcl jca ltplus1t syl13 leltt 3syl bicon2d mpbid
    sylan13 sylan9bbr adantl ltnr ltplus1 addrcl lelt bicon2 mpbi mp2an syl6bb
    ccase elun elsni imlor sylbi syl2an ) ACDZAEFZGZBCDZBEFZGZABHIZAJKLZBMIZNZA
    CEOZPZDZBXBDZWKWNWLWOWTABQWLWNRWQEJKLZBMIZWSWLWQEBHIZWNXFAEBHSWNXGXFWNXGTWN
    XFTBUAWNJBXEMBUBJUCUDUEUFUGUHWLWSXFNWNWLWRXEBMAEJKUIZUJUKULWKWORWQWREMIZWSW
    OWQAEHIZWKXIBEAHUMZXJUNXIUNZXJXINWKXJXIUOWKXJEAHIZAUAZWKAUPDZXJXMUNTZAUQZXO
    EUPDZXPURAEUSUTVAVBWKEWRHIZXLXOWRUPDZRXMAWRHIZRZXSWKXRXOXTYBXSTUREAWRVCVDWK
    XOXTXQWKXOXTXQXOJUPDXTVEAJVFUTZVAVGWKXMYAXNWKXOYAXQAVHVAVGVIWKXIXSWKXOXTXIX
    SUNNZXQYCXTXRYDURWREVJUTVKVLVMVNVOWOWSXINZWKBEWRMUMZVPULWLWORWQXIWSWOWQXJWL
    XIXKWLXJXEEMIZXIWLXJEEHIZYGAEEHSYHUNYGUNZYHYGNEURVQEXEHIZYIEURVRYGYJXEEEJUR
    VEVSURVTWAWBYHYGUOWCWDWLWRXEEMXHUJULVOWOYEWLYFVPULWEXCWKAXADZGWMACXAWFYKWLW
    KAEWGWHWIXDWNBXADZGWPBCXAWFYLWOWNBEWGWHWIWJ $.
    $([4-Dec-02]$)

  ${
    $d x y A $.  $d x y B $.
    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsub $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( vx vy clt wbr cmin co cn wcel wi cv c1 caddc wceq breq2 opreq1 eleq1d
      bi2imd weq cle wn nnge1t ax-mp ax1re nnre lelt mpbi pm2.21i wo cr wa wb
      nnret jctil leloet syl nnlelt1 mpan bitr3d cc nncnt nncn jctir 1cn
      addsubt mp3an2 peano2c syl5bir syl3d com23 cc0 addsub subid opreq1i
      addid2 3eqtr peano1c eqeltr opreq1d mpbii a1d a1i jaod sylbird ind
      nngt0t posdif sylib impbi ) ABGHZBAIJZKLZBKLWMWOMZDAENZGHZWQAIJZKLZMAOGHZ
      OAIJZKLZMAFNZGHZXDAIJZKLZMZAXDOPJZGHZXIAIJZKLZMWPEFBWQOQZWRXAWTXCWQOAGRXM
      WSXBKWQOAISTUAEFUBZWRXEWTXGWQXDAGRXNWSXFKWQXDAISTUAWQXIQZWRXJWTXLWQXIAGRX
      OWSXKKWQXIAISTUAWQBQZWRWMWTWOWQBAGRXPWSWNKWQBAISTUAXAXCOAUCHZXAUDAKLZXQCA
      UEUFOAUGACUHZUIUJUKXDKLZXJXHXLXTXJXEAXDQZULZXHXLMZXTAXDUCHZYBXJXTAUMLZXDU
      MLZUNYDYBUOXTYFYEXDUPXSUQAXDURUSXRXTYDXJUOCAXDUTVAVBXTXEYCYAXTXHXEXLXTXGX
      LXEXTXLXFOPJZKLXGXTXKYGKXTXDVCLZAVCLZUNXKYGQZXTYHYIXDVDACVEZVFYHOVCLYIYJV
      GXDOAVHVIUSTXFVJVKVLVMYAYCMXTYAXLXHYAAOPJZAIJZKLXLYMOKYMAAIJZOPJVNOPJOAOA
      YKVGYKVOYNVNOPAYKVPVQOVGVRVSVTWAYAYMXKKYAYLXIAIAXDOPSWBTWCWDWEWFWGVMWHUFW
      OVNWNGHWMWNWIBABDUHXSWJWKWL $.
      $([19-Aug-01]$)
  $}

  $( Subtraction of natural numbers. $)
  nnsubt $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cn wcel clt wbr cmin co wb c1 cded wceq breq1 opreq2 eleq1d bi2bid breq2
    opreq1 peano1c elimel nnsub dedth2h ) ACDZBCDZABEFZBAGHZCDZIUCAJKZBEFZBUHGH
    ZCDZIUHUDBJKZEFZULUHGHZCDZIABJJAUHLZUEUIUGUKAUHBEMUPUFUJCAUHBGNOPBULLZUIUMU
    KUOBULUHEQUQUJUNCBULUHGROPUHULAJCSTBJCSTUAUB $.
    $([20-Aug-01]$)

  $( Closure of addition of natural numbers minus one. $)
  nnaddm1clt $p |- ( ( A e. NN /\ B e. NN ) -> ( ( A + B ) - 1 ) e. NN ) $=
    ( cn wcel wa c1 caddc co clt wbr cmin cr cle wi ax1re addrcl ltletrt
    mp3an2 mpan axaddrcl nnret syl2an letrt mp3an1 syl adantl jca nnge1t wb
    leadd2t mpan2 mpbid adantr leadd1t syl13 ltplus1 jctil nnaddclt peano1c
    nnsubt ) ACDZBCDZEZFABGHZIJZVDFKHCDZVDLDZFFFGHZIJZVHVDMJZEZVEVCFLDZVGVKVENZ
    OVLVHLDZVGVMFFOOPZFVHVDQRSALDZBLDZVGVAVBABTAUAZBUAZUBZVCVJVIFBGHZLDZVGEVHWA
    MJZWAVDMJZEZVJVCVNWBVGWEVJNVOVHWAVDUCUDVCWBVGVBWBVAVBVQWBVSVLVQWBOFBTSUEUFV
    TUGVCWCWDVCFBMJZWCVBWFVABUHUFVCVQWFWCUIZVBVQVAVSUFVQVLWGOVLVQVLWGOFBFUJUDUK
    UEULVCFAMJZWDVAWHVBAUHUMVPVQWHWDUIZVAVBVLVPVQWIOFABUNUDVRVSUBULUGUOFOUPUQUO
    VCVDCDZVEVFUIZABURFCDWJWKUSFVDUTSUEUL $.
    $([6-Aug-03]$)

  ${
    $d x A $.  $d x B $.
    $( Two ways to express " ` A ` divides ` B ` for natural numbers. $)
    nndiv $p |- ( ( A e. NN /\ B e. NN ) ->
                ( E. x e. NN ( A x. x ) = B <-> ( B / A ) e. NN ) ) $=
      ( cn wcel wa cc0 wne cv cmulc co wceq wrex cdiv wb nnne0t adantr cc wi
      pm3.26 pm3.27d divcan3t exp nncnt syl5 com23 a1d imp41 cleqcomd opreq1
      sylan9eq eleq1d mpbid exp31 r19.23adv opreq2 cleq1d rcla4ev divcan2t imp
      sylan5 comm impbid syl2an mpd ) BDEZCDEZFBGHZBAIZJKZCLZADMZCBNKZDEZOZVFVH
      VGBPQBREZCREZVHVOSVFVGVPVQFZVHVOVRVHFZVLVNVSVKVNADVSVIDEZVKVNVSVTFZVKFZVT
      VNWBVSVTWAVKTUAWBVIVMDWAVKVIVJBNKZVMWAWCVIVPVQVHVTWCVILZVPVHVTWDSSVQVPVTV
      HWDVPVIREZVHWDSZVTVPWEWFBVIUBUCVIUDUEUFUGUHUIVJCBNUJUKULUMUNUOVNVSVLVNVSV
      LVNBVMJKZCLZVLVSVKWHAVMDVIVMLVJWGCVIVMBJUPUQURVRVHWHBCUSUTVAUCVBVCUCBUDCU
      DVDVE $.
      $([?]$)
  $}

  ${
    $d x y z w v A $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element.  Theorem I.37 (well-ordering principle) of [Apostol]
       p. 34. $)
    nnwo $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv vz cn wss c0 wne cv cle wbr wral wrex wceq wn wcel wal wa wi c1
      caddc co breq1 biraldv bilimd weq ssel nnge1t syl6 r19.21aiv adantr
      rcla4ev exp comm pm2.16d wb cr letri3t nnret syl2an clt nnlelt1 ancoms
      leltt ax1re axaddrcl mpan2 sylan bicon2d bitrd biland imp sylan5 eleq1a
      ad2antrr sylbird exp3a pm2.15 com23 exp32 com34 imp41 r19.20dva sylan9r
      pm2.43d exp31 imp3a a2d ind adantl syld adantrd pm2.61i 19.21adv nulleq
      syl6ibr pm2.15d df-ne syl5ib ) CGHZCIJZAKZBKZLMZBCNZACOZXBCIPZQXHXCXBXHXI
      XBXHQZDKZCRZQZDSXIXBXJXMDXBXJXMXKGRZXBXJTZXMUAXOXNXMXOXNXKXELMZBCNZXMXNXO
      XQXOEKZXELMZBCNZUAXOUBXELMZBCNZUAXOFKZXELMZBCNZUAXOYCUBUCUDZXELMZBCNZUAXO
      XQUAEFXKXRUBPZXTYBXOYIXSYABCXRUBXELUEUFUGEFUHZXTYEXOYJXSYDBCXRYCXELUEUFUG
      XRYFPZXTYHXOYKXSYGBCXRYFXELUEUFUGEDUHZXTXQXOYLXSXPBCXRXKXELUEUFUGXBYBXJXB
      YABCXBXECRZXEGRZYACGXEUIZXEUJUKULUMYCGRZXOYEYHYPXBXJYEYHUAZYPXBXJYQYPXBTZ
      XJTYEYHXJYEYCCRZQZYRYQYEXJYTYEYSXHYSYEXHYSYEXHXGYEAYCCAFUHXFYDBCXDYCXELUE
      UFUNUOUPUQUPYRYTYQYRYTTYDYGBCYPXBYTYMYDYGUAZYPXBYMYTUVAYPXBYMYTUVAUAYPXBY
      MTZTZYDYTYGUVCYDYGQZYSUAYTYGUAUVCYDUVDYSUVCYDUVDTZFBUHZYSYPYNUVFUVEURUVBY
      PYNTZUVFYDXEYCLMZTZUVEYCUSRZXEUSRZUVFUVIURYPYNYCXEUTYCVAZXEVAZVBUVGUVHUVD
      YDUVGUVHXEYFVCMZUVDYNYPUVHUVNURXEYCVDVEUVJUVKUVNUVDURYPYNUVJUVKTYGUVNYFUS
      RZUVKYGUVNQURUVJYFXEVFUVJUBUSRUVOVGYCUBVHVIVJVKUVLUVMVBVLVMVLXBYMYNYOVNVO
      YMUVFYSUAYPXBXECYCVPVQVRVSYGYSVTUKWAWBWCWDWEUOWFWGWHWIWJWKUPXJXQXMUAXBXQX
      JXMXQXLXHXLXQXHXLXQXHXGXQAXKCADUHXFXPBCXDXKXELUEUFUNUOUPUQUPWLWMUPXNQZXBX
      MXJXBUVPXMXBXLXNCGXKUIUQUPWNWOUOWPDCWQWRWSCIWTXAVN $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y z w v $.  $d w v z A $.
    nnwof.1 $e |- ( z e. A -> A. x z e. A ) $.
    nnwof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element.  This version allows ` x ` and ` y ` to be present in
       ` A ` as long as they are effectively not free. $)
    nnwof $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv cn wss c0 wne wa cv cle wbr wral wrex nnwo wcel wi wal wex wel
      ax-17 hbel hbim weq eleq1 breq2 bi2imd cbval bilan biex hbal hban breq1
      bilimd bialdv bi2and cbvex bitr df-rex 3bitr4 df-ral birex sylib ) DIJDKL
      MGNZHNZOPZHDQZGDRZANZBNZOPZBDQZADRZGHDSVIDTZVJUAZHUBZGDRZVNDTZVOUAZBUBZAD
      RZVLVQVHDTZVTMZGUCZVMDTZWDMZAUCZWAWEWHWFWBVHVNOPZUAZBUBZMZGUCWKWGWOGVTWNW
      FVSWMHBVRVJBBCCVIDCHUDBUEFUFVJBUEUGWMHUEHBUHVRWBVJWLVIVNDUIVIVNVHOUJUKULU
      MUNWOWJGAWFWNAACCVHDCGUDAUEEUFWMABWBWLAACCVNDCBUDAUEEUFWLAUEUGUOUPWJGUEGA
      UHZWFWIWNWDVHVMDUIWPWMWCBWPWLVOWBVHVMVNOUQURUSUTVAVBVTGDVCWDADVCVDVKVTGDV
      JHDVEVFVPWDADVOBDVEVFVDVG $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y z $.  $d y z ph $.  $d x ps $.
    nnwos.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element (schema form). $)
    nnwos $p |- ( E. x e. NN ph ->
                  E. x e. NN ( ph /\ A. y e. NN ( ps -> x <_ y ) ) ) $=
      ( vz cn crab wss c0 wne wa cv cle wbr wral wrex wi hbrab1 wcel ax-17
      nnwof wceq wn df-ne ssrab biantrur rnoel3 3bitr3 wex wal df-rex rabid
      df-ral elrab birim impexp bitr bial bi2an biex bilan anass bitr3 bitr4
      3bitr 3imtr3 ) ACGHZGIZVHJKZLZCMZDMZNOZDVHPZCVHQZACGQZABVNRZDGPZLZCGQZCDF
      VHACFGSFMVHTDUAUBVJVHJUCUDVKVQVHJUEVIVJACGUFUGACGUHUIVPVLVHTZVOLZCUJVLGTZ
      ALZVMGTZVRRZDUKZLZCUJZWAVOCVHULWCWICWBWEVOWHACGUMVOVMVHTZVNRZDUKWHVNDVHUN
      WLWGDWLWFBLZVNRWGWKWMVNABCVMGEUOUPWFBVNUQURUSURUTVAWJWDVTLZCUJWAWIWNCWIWE
      VSLWNVSWHWEVRDGUNVBWDAVSVCVDVAVTCGULVEVFVG $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    $( Substitutions. $)
    indstr.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Induction hypothesis. $)
    indstr.2 $e |- ( x e. NN -> ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference schema). $)
    indstr $p |- ( x e. NN -> ph ) $=
      ( cn wral wn wrex cv cle wbr wi wa wcel pm3.24 clt cr wb leltt nnret
      syl2an bilimd pm4.1 syl6bbr biraldva sylbid imland ancom syl6ib mtoi
      rgen ralnex mpbi weq binegd nnwos mto dfral2 mpbir rspec ) ACGACGHAIZCGJZ
      IVDVCBIZCKZDKZLMZNZDGHZOZCGJZVKIZCGHVLIVMCGVFGPZVKAVCOZAQVNVKVCAOVOVNVJAV
      CVNVJVGVFRMZBNZDGHAVNVIVQDGVNVGGPZOZVIVEVPIZNVQVSVHVTVEVFSPVGSPVHVTTVNVRV
      FVGUAVFUBVGUBUCUDVPBUEUFUGFUHUIVCAUJUKULUMVKCGUNUOVCVECDCDUPABEUQURUSACGU
      TVAVB $.
      $([17-Aug-01]$)
  $}

