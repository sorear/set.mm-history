$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( Our natural numbers include zero (some authors don't).  They are
       defined as the smallest inductive set containing zero. $)
    df-n  $a |- NN =
       |^| { x | ( 0 _10 e. x /\ A. y ( y e. x -> ( y + 1 _10 ) e. x ) ) } $.
  $}

  $( Define the set of integers. $)
  df-z $a |- ZZ = { x | ( x e. NN \/ -u x e. NN ) } $.

  ${
    $d x y z $.
    $( Define the set of rationals. $)
    df-q $a |- QQ =
                { x | E. y e. ZZ E. z e. ( ZZ \ { 0 _10 } ) x = ( y / z ) } $.
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    peano5c.1 $e |- A e. V $.
    $( Peano's inductive postulate. $)
    peano5c   $p |- ( ( 0 _10 e. A /\
                 A. x ( x e. A -> ( x + 1 _10 ) e. A ) ) -> NN (_ A ) $=
      ( vy cn0 wcel cv cn1 caddc co wi wal wa wel cab cint wss cn wceq eleq2
      bi2imd bialdv bi2and abcl intss sylbir df-n sseq1i sylibr ) EBFZAGZBFZUKH
      IJZBFZKZALZMZEDGZFZADNZUMURFZKZALZMZDOZPZBQZRBQUQBVEFVGVDUQDBCURBSZUSUJVC
      UPURBETVHVBUOAVHUTULVAUNURBUKTURBUMTUAUBUCUDBVEUEUFRVFBDAUGUHUI $.
      $([10-Jan-97]$)
  $}

  $( ` x ` is dummy. $)
  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN (_ RR $=
    ( vx cn0 cr wcel cv cn1 caddc co wi wal cn wss cc axcnex axresscn ssexi
    peano5c ax0re axaddrcl ax1re mpan2 ax-gen mp2an ) BCDAEZCDZUDFGHCDZIZAJKCLA
    CCMNOPQRUGAUEFCDUFUDFSTUAUBUC $.
    $([10-Jan-97]$)

  $( A natural number is a real number. $)
  nnret $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre ssi ) BCADE $.
    $([18-Aug-99]$)

  $( A natural number is a complex number. $)
  nncnt $p |- ( A e. NN -> A e. CC ) $=
    ( cn wcel cr cc nnret recnt syl ) ABCADCAECAFAGH $.
    $([18-Aug-99]$)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnre $p |- A e. RR $=
      ( cn wcel cr nnret ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)

    $( A natural number is a complex number. $)
    nncn $p |- A e. CC $=
      ( cn wcel cc nncnt ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)
  $}

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Peano postulate: 0 is a natural number. $)
    peano1c $p |- 0 _10 e. NN $=
      ( vx vy cn0 cn wcel cv wel cn1 caddc co wi wal wa cab cint df-n eleq2i
      cc 0cn elisseti inteqab pm3.26 mpgbir mpbir ) CDECCAFZEZBAGBFHIJUEEKBLZMZ
      ANOZEZDUICABPQUJUHUFKAUHACCRSTUAUFUGUBUCUD $.
      $([11-Jan-97]$)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2c $p |- ( A e. NN -> ( A + 1 _10 ) e. NN ) $=
      ( vz vx vy cv cn1 caddc co cn wcel wceq opreq1 eleq1d cn0 wel wi wal wa
      cab cint weq a13b bi2imd a4b1 adantl a2i 19.20i visset inteqab oprex
      3imtr4 df-n eleq2i vtoclga ) BEZFGHZIJZAFGHZIJBAIUOAKUPURIUOAFGLMUONCEZJZ
      DCOZDEZFGHZUSJZPZDQZRZCSTZJZUPVHJZUOIJUQVGBCOZPZCQVGUPUSJZPZCQVIVJVLVNCVG
      VKVMVFVKVMPZUTVEVODBDBUAZVAVKVDVMDBCUBVPVCUPUSVBUOFGLMUCUDUEUFUGVGCUOBUHU
      IVGCUPUOFGUJUIUKIVHUOCDULZUMIVHUPVQUMUKUN $.
      $([11-Jan-97]$)

  $}

  $( 1 is a natural number. $)
  1cnn $p |- 1 _10 e. NN $=
    ( cn0 cn1 caddc co cn wcel 1cn addid2 eleq1i peano1c peano2c ax-mp mpbi )
    ABCDZEFZBEFNBEBGHIAEFOJAKLM $.
    $([12-Jan-97]$)

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    ind.1 $e |- ( x = 0 _10 -> ( ph <-> ps ) ) $.
    ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    ind.3 $e |- ( x = ( y + 1 _10 ) -> ( ph <-> th ) ) $.
    ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    ind.5 $e |- ps $.
    $( Induction hypothesis. $)
    ind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The first
       four hypotheses give us the substitution instances we need; the last
       two are the basis and the induction hypothesis.  See ~nnaddclt for
       an example of its use. $)
    ind $p |- ( A e. NN -> ta ) $=
      ( cn wcel cv wa cab wceq eleq1 bi2and abclg cn0 cn1 caddc co wi wal wss
      cr cc axcnex axresscn ssexi nnssre zfausab peano5c 0cn elisseti abcl
      peano1c pm3.2i mpbir abid2 eleq2i peano2c a1i im2and visset weq oprex
      3imtr4g sylbir pm3.26 ssab2i ssi mpcom ax-gen mp2an mpbid pm3.27d ) HOPZW
      CEWCHFQZOPZARZFSZPWCERZWFWHFHOWDHTWEWCAEWDHOUALUBUCOWGHUDWGPZGQZWGPZWJUEU
      FUGZWGPZUHZGUIOWGUJGWGAFOOUKUKULUMUNUOUPUOUQURWIUDOPZBRZWFWPFUDUDULUSUTWD
      UDTWEWOABWDUDOUAIUBVAWOBVBMVCVDWNGWJWEFSZPZWKWMWRWJOPZWNOWQWJFOVEVFWSWSCR
      ZWLOPZDRZWKWMWSWSXACDWSXAUHWSWJVGVHNVIWFWTFWJGVJFGVKWEWSACWDWJOUAJUBVAWFX
      BFWLWJUEUFVLWDWLTWEXAADWDWLOUAKUBVAVMVNWGWQWJWFWEFWEAVOVPVQVRVSVTVQWAWB
      $.
      $([10-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi cn0 cn1 wceq opreq2 eleq1d bilimd weq cc
      ax0id biimprd nncnt mpcom wa axaddass 1cn mp3an3 syl2an peano2c syl5bi
      exp comm a2d ind imp ) AEFZBEFZABGHZEFZUPUOURUOACIZGHZEFZJUOAKGHZEFZJUOAD
      IZGHZEFZJUOAVDLGHZGHZEFZJUOURJCDBUSKMZVAVCUOVJUTVBEUSKAGNOPCDQZVAVFUOVKUT
      VEEUSVDAGNOPUSVGMZVAVIUOVLUTVHEUSVGAGNOPUSBMZVAURUOVMUTUQEUSBAGNOPARFZUOV
      CVNVCUOVNVBAEASOTAUAZUBVDEFZUOVFVIUOVPVFVIJZUOVPVQUOVPUCZVELGHZEFVIVFVRVS
      VHEVNVDRFZVSVHMZUOVPVNVTLRFWAAVDLUDUEUFVOVDUAUGOVEUHUIUJUKULUMUKUN $.
      $([12-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmulclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmulc co cv wi cn0 cn1 caddc wceq opreq2 eleq1d bilimd
      weq cc nncnt cded opreq1 cleq1d 0cn elimel mulzer1 dedth syl peano1c
      mpbiri wa axdistr 1cn mp3an3 ax1id opreq2d adantr eqtrd syl2an nnaddclt
      ancoms syl5bir exp4b pm2.43b a2d ind imp ) BEFZAEFZABGHZEFZVHVIVKVIACIZGH
      ZEFZJVIAKGHZEFZJVIADIZGHZEFZJVIAVQLMHZGHZEFZJVIVKJCDBVLKNZVNVPVIWCVMVOEVL
      KAGOPQCDRZVNVSVIWDVMVREVLVQAGOPQVLVTNZVNWBVIWEVMWAEVLVTAGOPQVLBNZVNVKVIWF
      VMVJEVLBAGOPQVIVPKEFVIVOKEVIASFZVOKNZATZWGWHWGAKUAZKGHZKNAKAWJNVOWKKAWJKG
      UBUCWJAKSUDUEUFUGUHPUIUJVQEFZVIVSWBWLVIVSWBJVIWLVIVSWBVIWLUKZWBVRAMHZEFZV
      IVSUKWMWAWNEWGVQSFZWAWNNVIWLWGWPUKWAVRALGHZMHZWNWGWPLSFWAWRNAVQLULUMUNWGW
      RWNNWPWGWQAVRMAUOUPUQURWIVQTUSPVSVIWOVRAUTVAVBVCVDVEVFVGVA $.
      $([12-Jan-97]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number g.e. than any two others. $)
    nn2get $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cle wbr wo cv wrex wi cr nnret leidt biantrud biimpd syl
      anc2li wceq breq2 bi2and rcla4ev syl6 adantl biantrurd adantr jaod
      letrit syl2an mpd ) BDEZCDEZFZBCGHZCBGHZIZBAJZGHZCURGHZFZADKZUNUOVBUPUMUO
      VBLULUMUOUMUOCCGHZFZFVBUMUOVDUMCMEZUOVDLCNZVEUOVDVEVCUOCOPQRSVAVDACDURCTU
      SUOUTVCURCBGUAURCCGUAUBUCUDUEULUPVBLUMULUPULBBGHZUPFZFVBULUPVHULBMEZUPVHL
      BNZVIUPVHVIVGUPBOUFQRSVAVHABDURBTUSVGUTUPURBBGUAURBCGUAUBUCUDUGUHVIVEUQUL
      UMBCUIVJVFUJUK $.
      $([18-Aug-99]$)
  $}


