$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Natural numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The natural numbers of analysis start at one (unlike the natural
       numbers of set theory, i.e. the members of set omega, which start at
       zero).  This is the convention used by most analysis books.  See
       ~ ind for the principle of mathematical induction. $)
    df-n  $a |- NN =
       |^| { x | ( 1_10 e. x /\ A. y ( y e. x -> ( y + 1_10 ) e. x ) ) } $.
  $}

  ${
    $d x y A $.  $( ` y ` is dummy. $)
    peano5c.1 $e |- A e. V $.
    $( Peano's inductive postulate. $)
    peano5c   $p |- ( ( 1_10 e. A /\
                 A. x ( x e. A -> ( x + 1_10 ) e. A ) ) -> NN (_ A ) $=
      ( vy cn1 wcel cv caddc co wi wal wa wel cab cint wss cn wceq eleq2
      bi2imd bialdv bi2and abcl intss sylbir df-n sseq1i sylibr ) EBFZAGZBFZUJE
      HIZBFZJZAKZLZEDGZFZADMZULUQFZJZAKZLZDNZOZBPZQBPUPBVDFVFVCUPDBCUQBRZURUIVB
      UOUQBESVGVAUNAVGUSUKUTUMUQBUJSUQBULSTUAUBUCBVDUDUEQVEBDAUFUGUH $.
      $([10-Jan-97]$)
  $}

  $( ` x ` is dummy. $)
  $( The natural numbers are a subset of the reals. $)
  nnssre $p |- NN (_ RR $=
    ( vx cn1 cr wcel cv caddc co wi wal cn wss ax1re axaddrcl mpan2 ax-gen cc
    axcnex axresscn ssexi peano5c mp2an ) BCDZAEZCDZUCBFGCDZHZAIJCKLUFAUDUBUELU
    CBMNOACCPQRSTUA $.
    $([10-Jan-97]$)

  $( A natural number is a real number. $)
  nnret $p |- ( A e. NN -> A e. RR ) $=
    ( cn cr nnssre ssi ) BCADE $.
    $([18-Aug-99]$)

  $( A natural number is a complex number. $)
  nncnt $p |- ( A e. NN -> A e. CC ) $=
    ( cn wcel nnret recnd ) ABCAADE $.
    $([18-Aug-99]$)

  ${
    nnre.1 $e |- A e. NN $.
    $( A natural number is a real number. $)
    nnre $p |- A e. RR $=
      ( cn wcel cr nnret ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)

    $( A natural number is a complex number. $)
    nncn $p |- A e. CC $=
      ( cn wcel cc nncnt ax-mp ) ACDAEDBAFG $.
      $([18-Aug-99]$)
  $}

  $( The set of natural numbers exists. $)
  nnex $p |- NN e. V $=
    ( cn cr cc axcnex axresscn ssexi nnssre ) ABBCDEFGF $.
    $([3-Oct-99]$)

  ${
    $d x y z A $.  $( ` x y z ` are dummy. $)
    $( Peano postulate: 1 is a natural number. $)
    peano1c $p |- 1_10 e. NN $=
      ( vx vy cn1 cn wcel cv wel caddc co wi wal wa cab cint cc 1cn elisseti
      inteqab pm3.26 mpgbir df-n eleq2i mpbir ) CDECCAFZEZBAGBFCHIUDEJBKZLZAMNZ
      EZUIUGUEJAUGACCOPQRUEUFSTDUHCABUAUBUC $.
      $([11-Jan-97]$)

    $( Peano postulate: a successor of a natural number is a natural number. $)
    peano2c $p |- ( A e. NN -> ( A + 1_10 ) e. NN ) $=
      ( vz vx vy cv cn1 caddc co cn wcel wceq opreq1 eleq1d wel wi wal wa cab
      cint weq a13b bi2imd a4b1 adantl a2i 19.20i visset inteqab oprex 3imtr4
      df-n eleq2i vtoclga ) BEZFGHZIJZAFGHZIJBAIUNAKUOUQIUNAFGLMUNFCEZJZDCNZDEZ
      FGHZURJZOZDPZQZCRSZJZUOVGJZUNIJUPVFBCNZOZCPVFUOURJZOZCPVHVIVKVMCVFVJVLVEV
      JVLOZUSVDVNDBDBTZUTVJVCVLDBCUAVOVBUOURVAUNFGLMUBUCUDUEUFVFCUNBUGUHVFCUOUN
      FGUIUHUJIVGUNCDUKZULIVGUOVPULUJUM $.
      $([11-Jan-97]$)

  $}

  ${
    $d x y $.  $d x A $.  $d x ps $.  $d x ch $.  $d x th $.  $d x ta $.
    $d y ph $.
    $( Substitutions. $)
    ind.1 $e |- ( x = 1_10 -> ( ph <-> ps ) ) $.
    ind.2 $e |- ( x = y -> ( ph <-> ch ) ) $.
    ind.3 $e |- ( x = ( y + 1_10 ) -> ( ph <-> th ) ) $.
    ind.4 $e |- ( x = A -> ( ph <-> ta ) ) $.
    $( Basis. $)
    ind.5 $e |- ps $.
    $( Induction hypothesis. $)
    ind.6 $e |- ( y e. NN -> ( ch -> th ) ) $.
    $( Principle of Mathematical Induction (inference schema).  The first
       four hypotheses give us the substitution instances we need; the last
       two are the basis and the induction hypothesis.  See ~ nnaddclt for
       an example of its use. $)
    ind $p |- ( A e. NN -> ta ) $=
      ( cn wcel cv wa cab cn1 caddc co wi wal wss peano1c pm3.2i cc 1cn
      elisseti wceq eleq1 bi2and abcl mpbir pm3.26 ssab2i ssi abid2 eleq2i
      peano2c a1i im2and visset weq oprex 3imtr4g sylbir mpcom ax-gen nnex
      zfausab peano5c mp2an abclg mpbid pm3.27d ) HOPZVREVRHFQZOPZARZFSZPVRERZO
      WBHTWBPZGQZWBPZWETUAUBZWBPZUCZGUDOWBUEWDTOPZBRZWJBUFMUGWAWKFTTUHUIUJVSTUK
      VTWJABVSTOULIUMUNUOWIGWEVTFSZPZWFWHWBWLWEWAVTFVTAUPUQURWMWEOPZWIOWLWEFOUS
      UTWNWNCRZWGOPZDRZWFWHWNWNWPCDWNWPUCWNWEVAVBNVCWAWOFWEGVDFGVEVTWNACVSWEOUL
      JUMUNWAWQFWGWETUAVFVSWGUKVTWPADVSWGOULKUMUNVGVHVIVJGWBAFOVKVLVMVNURWAWCFH
      OVSHUKVTVRAEVSHOULLUMVOVPVQ $.
      $([10-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of addition of natural numbers, proved by induction on the
       second addend. $)
    nnaddclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A + B ) e. NN ) $=
      ( vx vy cn wcel caddc co cv wi cn1 wceq opreq2 eleq1d bilimd weq peano2c
      wa cc 1cn axaddass mp3an3 nncnt syl2an syl5bi exp comm a2d ind imp ) AEFZ
      BEFZABGHZEFZULUKUNUKACIZGHZEFZJUKAKGHZEFZJUKADIZGHZEFZJUKAUTKGHZGHZEFZJUK
      UNJCDBUOKLZUQUSUKVFUPUREUOKAGMNOCDPZUQVBUKVGUPVAEUOUTAGMNOUOVCLZUQVEUKVHU
      PVDEUOVCAGMNOUOBLZUQUNUKVIUPUMEUOBAGMNOAQUTEFZUKVBVEUKVJVBVEJZUKVJVKUKVJR
      ZVAKGHZEFVEVBVLVMVDEASFZUTSFZVMVDLZUKVJVNVOKSFVPTAUTKUAUBAUCUTUCUDNVAQUEU
      FUGUHUIUGUJ $.
      $([12-Jan-97]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` x y ` are dummy. $)
    $( Closure of multiplication of natural numbers. $)
    nnmulclt $p |- ( ( A e. NN /\ B e. NN ) -> ( A x. B ) e. NN ) $=
      ( vx vy cn wcel cmulc co cv wi cn1 caddc wceq opreq2 eleq1d bilimd weq
      cc nncnt ax1id biimprd mpcom wa 1cn axdistr mp3an3 opreq2d adantr eqtrd
      syl2an nnaddclt ancoms syl5bir exp4b pm2.43b a2d ind imp ) BEFZAEFZABGHZE
      FZUSUTVBUTACIZGHZEFZJUTAKGHZEFZJUTADIZGHZEFZJUTAVHKLHZGHZEFZJUTVBJCDBVCKM
      ZVEVGUTVNVDVFEVCKAGNOPCDQZVEVJUTVOVDVIEVCVHAGNOPVCVKMZVEVMUTVPVDVLEVCVKAG
      NOPVCBMZVEVBUTVQVDVAEVCBAGNOPARFZUTVGASZVRVGUTVRVFAEATZOUAUBVHEFZUTVJVMWA
      UTVJVMJUTWAUTVJVMUTWAUCZVMVIALHZEFZUTVJUCWBVLWCEVRVHRFZVLWCMUTWAVRWEUCVLV
      IVFLHZWCVRWEKRFVLWFMUDAVHKUEUFVRWFWCMWEVRVFAVILVTUGUHUIVSVHSUJOVJUTWDVIAU
      KULUMUNUOUPUQURUL $.
      $([12-Jan-97]$)
  $}

  ${
    $d x A $.  $d x B $.
    $( There exists a natural number g.e. than any two others. $)
    nn2get $p |- ( ( A e. NN /\ B e. NN ) ->
                E. x e. NN ( A <_ x /\ B <_ x ) ) $=
      ( cn wcel wa cle wbr wo cv wrex cr letrit nnret syl2an wi leidt biantrud
      biimpd syl anc2li wceq breq2 bi2and rcla4ev syl6 adantl biantrurd adantr
      jaod mpd ) BDEZCDEZFZBCGHZCBGHZIZBAJZGHZCURGHZFZADKZBLEZCLEZUQULUMBCMBNZC
      NZOUNUOVBUPUMUOVBPULUMUOUMUOCCGHZFZFVBUMUOVHUMVDUOVHPVFVDUOVHVDVGUOCQRSTU
      AVAVHACDURCUBUSUOUTVGURCBGUCURCCGUCUDUEUFUGULUPVBPUMULUPULBBGHZUPFZFVBULU
      PVJULVCUPVJPVEVCUPVJVCVIUPBQUHSTUAVAVJABDURBUBUSVIUTUPURBBGUCURBCGUCUDUEU
      FUIUJUK $.
      $([18-Aug-99]$)
  $}

  ${
    $d x y A $.
    $( A natural number is one or greater. $)
    nnge1t $p |- ( A e. NN -> 1_10 <_ A ) $=
      ( vx vy cn1 cv cle wbr caddc co breq2 wceq cleqid ax1re eqle ax-mp cn
      wcel cr wi nnret cn0 cc recnt ax0id syl breq2d clt wn lt01 wa jctl ax0re
      axltadd mp3an1 mpi axlttrn mp3an3 axaddrcl mpan2 sylan13 exp3a mpd
      pm2.16d wb jctil leltt 3imtr4d sylbird ind ) DBEZFGDDFGZDCEZFGZDVLDHIZFGZ
      DAFGBCAVJDDFJVJVLDFJVJVNDFJVJADFJDDKVKDLDDMMNOVLPQVLRQZVMVOSVLTVPVMDVLUAH
      IZFGZVOVPVQVLDFVPVLUBQVQVLKVLUCVLUDUEUFVPVQDUGGZUHZVNDUGGZUHZVRVOVPWAVSVP
      VQVNUGGZWAVSSVPUADUGGZWCUIVPDRQZVPUJWDWCSZVPWEMUKUARQZWEVPWFULUADVLUMUNUE
      UOVPWCWAVSVQRQZVNRQZWCWAUJVSSZVPWHWIWEWJMVQVNDUPUQVPWGWHULVLUAURUSZVPWEWI
      MVLDURUSZUTVAVBVCVPWEWHUJVRVTVDVPWHWEWKMVEDVQVFUEVPWEWIUJVOWBVDVPWIWEWLMV
      EDVNVFUEVGVHUEVI $.
      $([25-Aug-99]$)
  $}

  $( A natural number is positive. $)
  nngt0t $p |- ( A e. NN -> 0_10 < A ) $=
    ( cr wcel cn1 cle wbr cn0 clt cn lt01 wa wi ax1re ax0re ltletrt mp3an1
    mpan mpani nnret nnge1t syl13 ) ABCZDAEFZGAHFZAICUBGDHFZUCUDJDBCZUBUEUCKUDL
    ZMGBCUFUBUGNGDAOPQRASATUA $.
    $([26-Sep-99]$)

  $( Zero is not a natural number. $)
  0nnn $p |- -. 0_10 e. NN $=
    ( cn0 cn wcel cn1 cle wbr clt wn lt01 ax1re ax0re lelt bicon2 mpbi nnge1t
    mto ) ABCDAEFZADGFZQHIQRDAJKLMNAOP $.
    $([25-Aug-99]$)

  $( A natural number is non-zero. $)
  nnne0t $p |- ( A e. NN -> A =/= 0_10 ) $=
    ( cn wcel cn0 wceq wn wne 0nnn eleq1 mtbiri con2 df-ne sylibr ) ABCZADEZFAD
    GONONDBCHADBIJKADLM $.
    $([27-Sep-99]$)

  ${
    nngt0.1 $e |- A e. NN $.
    $( A natural number is positive. $)
    nngt0 $p |- 0_10 < A $=
      ( cn wcel cn0 clt wbr nngt0t ax-mp ) ACDEAFGBAHI $.
      $([17-Sep-99]$)

    $( A natural number is non-zero. $)
    nnne0 $p |- A =/= 0_10 $=
      ( nnre nngt0 gt0ne0i ) AABCABDE $.
      $([25-Aug-99]$)
  $}

  ${
    $d x y z w v A $.  $d x y z w v B $.
    $( Natural number ordering relation. $)
    nnlelt1 $p |- ( ( A e. NN /\ B e. NN ) ->
                  ( A <_ B <-> A < ( B + 1_10 ) ) ) $=
      ( vz vx vy vw vv cn wcel wa cle wbr clt wceq wo cn1 caddc co cr wb
      leloet nnret syl2an wi cn0 ax0re ax1re pm3.2i lt2addt an4s mpan2 lt01
      jctr syl5 cc pm3.26 recnt ax0id 3syl breq1d sylibd breq1 syl ltadd2t
      mp3an1 mpan mpbii eqbrtrrd syl5bir comm adantl jaod cv wral weq opreq1
      breq2d breq2 cleq2 bi2ord bi2imd biraldv cleq1 wn cleqid a1i orri w3a
      3jca ltadd1t nnge1t leltt sylan13 mpbid pm2.21d sylbird a1d ind rgen
      bilimd nngt0t 1cn addid2 breq1i sylib pm2.15d orrd adantr rcla4v imp
      adantalr jctir axaddrcl im3an 3anrot sylibr addcant bicomd 3simpc 3simp1
      jca anandir axaddcom cleqan12d bitr4d 3imtr3d exp31 imp3a exp3a com3l
      r19.21adv rcbvalv syl5ib impbid bitrd ) AHIZBHIZJZABKLZABMLZABNZOZABPQRZM
      LZASIZBSIZYSUVBTYPYQABUAAUBZBUBZUCYRUVBUVDUVEUVFUVBUVDUDYPYQUVEUVFJZYTUVD
      UVAUVIYTAUEQRZUVCMLZUVDUVIYTUEPMLZJZUVKYTUVIUESIZPSIZJUVMUVKUDZUVNUVOUFUG
      UHUVEUVNUVFUVOUVPAUEBPUIUJUKYTUVLULUMUNUVIUVJAUVCMUVIUVEAUOIUVJANUVEUVFUP
      AUQAURUSUTVAUVFUVAUVDUDUVEUVAUVFUVDUVAUVDBUVCMLUVFABUVCMVBUVFBUEQRZBUVCMU
      VFBUOIUVQBNBUQBURVCUVFUVLUVQUVCMLZULUVOUVFUVLUVRTZUGUVNUVOUVFUVSUFUEPBVDV
      EVFVGVHVIVJVKVLUVGUVHUCYPYQUVDUVBUDZYQYPUVTYQCVMZUVCMLZUWABMLZUWABNZOZUDZ
      CHVNZYPUVTUDUWADVMZPQRZMLZUWAUWHMLZCDVOZOZUDZCHVNUWAPPQRZMLZUWAPMLZUWAPNZ
      OZUDZCHVNUWAEVMZPQRZMLZUWAUXAMLZCEVOZOZUDZCHVNZUWAUXBPQRZMLZUXCUWAUXBNZOZ
      UDZCHVNZUWGDEBUWHPNZUWNUWTCHUXOUWJUWPUWMUWSUXOUWIUWOUWAMUWHPPQVPVQUXOUWKU
      WQUWLUWRUWHPUWAMVRUWHPUWAVSVTWAWBDEVOZUWNUXGCHUXPUWJUXCUWMUXFUXPUWIUXBUWA
      MUWHUXAPQVPVQUXPUWKUXDUWLUXEUWHUXAUWAMVRUWHUXAUWAVSVTWAWBUWHUXBNZUWNUXMCH
      UXQUWJUXJUWMUXLUXQUWIUXIUWAMUWHUXBPQVPVQUXQUWKUXCUWLUXKUWHUXBUWAMVRUWHUXB
      UWAVSVTWAWBUWHBNZUWNUWFCHUXRUWJUWBUWMUWEUXRUWIUVCUWAMUWHBPQVPVQUXRUWKUWCU
      WLUWDUWHBUWAMVRUWHBUWAVSVTWAWBUWTCHUWHUWOMLZUWHPMLZUXOOZUDPUWOMLZPPMLZPPN
      ZOZUDUXAUWOMLZUXAPMLZUXAPNZOZUDZUXBUWOMLZUXBPMLZUXBPNZOZUDZUWTDEUWAUXOUXS
      UYBUYAUYEUWHPUWOMVBUXOUXTUYCUXOUYDUWHPPMVBUWHPPWCVTWAUXPUXSUYFUYAUYIUWHUX
      AUWOMVBUXPUXTUYGUXOUYHUWHUXAPMVBUWHUXAPWCVTWAUXQUXSUYKUYAUYNUWHUXBUWOMVBU
      XQUXTUYLUXOUYMUWHUXBPMVBUWHUXBPWCVTWADCVOZUXSUWPUYAUWSUWHUWAUWOMVBUYPUXTU
      WQUXOUWRUWHUWAPMVBUWHUWAPWCVTWAUYEUYBUYCUYDUYDUYCWDPWEWFWGWFUXAHIZUYOUYJU
      YQUYKUYGUYNUYQUXASIZUVOUVOWHUYGUYKTUYQUYRUVOUVOUXAUBZUVOUYQUGWFZUYTWIUXAP
      PWJVCUYQUYGUYNUYQPUXAKLZUYGWDZUXAWKUVOUYRVUAVUBTUYQPUXAWLUYTUYSWMWNWOWPWQ
      WRWSUYQFVMZUXBMLZVUCUXAMLZFEVOZOZUDZFHVNZUXNUXHUYQVUIUXMCHUWAHIZUYQVUIUXM
      VUJUYQVUIUXMUYQVUIJZUWHUXIMLZUWHUXBMLZUXQOZUDZUDVUKPUXIMLZPUXBMLZPUXBNZOZ
      UDZUDVUKGVMZUXIMLZVVAUXBMLZVVAUXBNZOZUDZUDZVUKVVAPQRZUXIMLZVVHUXBMLZVVHUX
      BNZOZUDZUDZVUKUXMUDDGUWAUXOVUOVUTVUKUXOVULVUPVUNVUSUWHPUXIMVBUXOVUMVUQUXQ
      VURUWHPUXBMVBUWHPUXBWCVTWAWTDGVOZVUOVVFVUKVVOVULVVBVUNVVEUWHVVAUXIMVBVVOV
      UMVVCUXQVVDUWHVVAUXBMVBUWHVVAUXBWCVTWAWTUWHVVHNZVUOVVMVUKVVPVULVVIVUNVVLU
      WHVVHUXIMVBVVPVUMVVJUXQVVKUWHVVHUXBMVBUWHVVHUXBWCVTWAWTUYPVUOUXMVUKUYPVUL
      UXJVUNUXLUWHUWAUXIMVBUYPVUMUXCUXQUXKUWHUWAUXBMVBUWHUWAUXBWCVTWAWTUYQVUTVU
      IUYQVUSVUPUYQVUQVURUYQVURVUQUYQVUQVURWDUYQUEPQRZUXBMLZVUQUYQUEUXAMLZVVRUX
      AXAUYQUVNUYRUVOWHVVSVVRTUYQUVNUYRUVOUVNUYQUFWFUYSUYTWIUEUXAPWJVCWNVVQPUXB
      MPXBXCXDXEWQXFXGWQXHVVAHIZVVNVVGVVTUYQVUIVVMVVTUYQVUIVVMVVTUYQJZVUIJVVCVV
      AUXAMLZGEVOZOZVVIVVLVVTVUIVVCVWDUDZUYQVVTVUIVWEVUIVVTVWEVUHVWEFVVAHFGVOZV
      UDVVCVUGVWDVUCVVAUXBMVBVWFVUEVWBVUFVWCVUCVVAUXAMVBVUCVVAUXAWCVTWAXIVJXJXK
      VWAVVCVVITZVUIVWAVVASIZUXBSIZUVOWHVWGVWAVWHVWIUVOVVTVWHUYQVVAUBXHZUYQVWIV
      VTUYQUYRUVOJVWIUYQUYRUVOUYSUGXLUXAPXMVCVKUVOVWAUGWFZWIVVAUXBPWJVCXHVWAVWD
      VVLTZVUIVWAVWHUYRUVOWHZVWLVWAVWHUYRUVOVWJUYQUYRVVTUYSVKVWKWIVWMVWBVVJVWCV
      VKVVAUXAPWJVWMPUOIZVVAUOIZUXAUOIZWHZVWCVVKTVWMVWOVWPVWNWHVWQVWHVWOUYRVWPU
      VOVWNVVAUQUXAUQPUQXNVWNVWOVWPXOXPVWQVWCPVVAQRZPUXAQRZNZVVKVWQVWTVWCPVVAUX
      AXQXRVWQVWOVWNJZVWPVWNJZJZVVKVWTTVWQVWOVWPJZVWNJVXCVWQVXDVWNVWNVWOVWPXSVW
      NVWOVWPXTYAVWOVWPVWNYBXEVXAVXBVVHVWRUXBVWSVVAPYCUXAPYCYDVCYEVCVTVCXHYFYGY
      HWQWRYIYJYKUXGVUHCFHCFVOZUXCVUDUXFVUGUWAVUCUXBMVBVXEUXDVUEUXEVUFUWAVUCUXA
      MVBUWAVUCUXAWCVTWAYLYMWRUWFUVTCAHUWAANZUWBUVDUWEUVBUWAAUVCMVBVXFUWCYTUWDU
      VAUWAABMVBUWAABWCVTWAXIVCVJXJYNYO $.
      $([13-Aug-01]$)
  $}


  $( Natural number ordering relation. $)
  nnltle1 $p |- ( ( A e. NN /\ B e. NN ) ->
                ( A < B <-> ( A + 1_10 ) <_ B ) ) $=
    ( cn wcel wa clt wbr cn1 caddc co cle cr wb ax1re ltadd1t mp3an3 nnret
    syl2an nnlelt1 peano2c sylan bitr4d ) ACDZBCDZEABFGZAHIJZBHIJFGZUFBKGZALDZB
    LDZUEUGMZUCUDUIUJHLDUKNABHOPAQBQRUFCDUDUHUGMUCUFBSATUAUB $.
    $([19-Aug-01]$)

  ${
    $d x y A $.  $d x y B $.
    nnsub.1 $e |- A e. NN $.
    nnsub.2 $e |- B e. NN $.
    $( Subtraction of natural numbers. $)
    nnsub $p |- ( A < B <-> ( B - A ) e. NN ) $=
      ( vx vy clt wbr cmin co cn wcel wi cv cn1 caddc wceq breq2 opreq1 eleq1d
      bi2imd weq cle wn nnge1t ax-mp ax1re nnre lelt mpbi pm2.21i wo cr wa wb
      nnret jctil leloet syl nnlelt1 mpan bitr3d cc recnd recn jctir 1cn
      addsubt mp3an2 peano2c syl5bir syl3d com23 peano1c cn0 addsub subid
      opreq1i addid2 3eqtr eleq1i mpbir opreq1d mpbii a1d a1i jaod sylbird ind
      nngt0t posdif sylib impbi ) ABGHZBAIJZKLZBKLWNWPMZDAENZGHZWRAIJZKLZMAOGHZ
      OAIJZKLZMAFNZGHZXEAIJZKLZMZAXEOPJZGHZXJAIJZKLZMWQEFBWROQZWSXBXAXDWROAGRXN
      WTXCKWROAISTUAEFUBZWSXFXAXHWRXEAGRXOWTXGKWRXEAISTUAWRXJQZWSXKXAXMWRXJAGRX
      PWTXLKWRXJAISTUAWRBQZWSWNXAWPWRBAGRXQWTWOKWRBAISTUAXBXDOAUCHZXBUDAKLZXRCA
      UEUFOAUGACUHZUIUJUKXEKLZXKXIXMYAXKXFAXEQZULZXIXMMZYAAXEUCHZYCXKYAAUMLZXEU
      MLZUNYEYCUOYAYGYFXEUPZXTUQAXEURUSXSYAYEXKUOCAXEUTVAVBYAXFYDYBYAXIXFXMYAXH
      XMXFYAXMXGOPJZKLXHYAXLYIKYAXEVCLZAVCLZUNXLYIQZYAYJYKYAXEYHVDAXTVEZVFYJOVC
      LYKYLVGXEOAVHVIUSTXGVJVKVLVMYBYDMYAYBXMXIYBAOPJZAIJZKLZXMYPOKLVNYOOKYOAAI
      JZOPJVOOPJOAOAYMVGYMVPYQVOOPAYMVQVROVGVSVTWAWBYBYOXLKYBYNXJAIAXEOPSWCTWDW
      EWFWGWHVMWIUFWPVOWOGHWNWOWJBABDUHXTWKWLWM $.
      $([19-Aug-01]$)
  $}

  $( Subtraction of natural numbers. $)
  nnsubt $p |- ( ( A e. NN /\ B e. NN ) -> ( A < B <-> ( B - A ) e. NN ) ) $=
    ( cn wcel clt wbr cmin co wb cn1 cded wceq breq1 opreq2 eleq1d bi2bid
    breq2 opreq1 peano1c elimel nnsub dedth2h ) ACDZBCDZABEFZBAGHZCDZIUCAJKZBEF
    ZBUHGHZCDZIUHUDBJKZEFZULUHGHZCDZIABJJAUHLZUEUIUGUKAUHBEMUPUFUJCAUHBGNOPBULL
    ZUIUMUKUOBULUHEQUQUJUNCBULUHGROPUHULAJCSTBJCSTUAUB $.
    $([20-Aug-01]$)

  ${
    $d x y z w v A $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element. $)
    nnwo $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv vz cn wss c0 wne cv cle wbr wral wrex wceq wn wcel wal wa wi cn1
      caddc co breq1 biraldv bilimd weq ssel nnge1t syl6 r19.21aiv adantr
      rcla4ev exp comm pm2.16d wb cr letri3t nnret syl2an clt nnlelt1 ancoms
      leltt ax1re axaddrcl mpan2 sylan bicon2d bitrd biland imp sylan5 eleq1a
      adantl sylbird exp3a pm2.15 com23 exp32 com34 imp41 r19.20dva sylan9r
      pm2.43d exp31 imp3a a2d ind syld adantrd pm2.61i 19.21adv nulleq syl6ibr
      pm2.15d df-ne syl5ib ) CGHZCIJZAKZBKZLMZBCNZACOZXACIPZQXGXBXAXGXHXAXGQZDK
      ZCRZQZDSXHXAXIXLDXAXIXLXJGRZXAXITZXLUAXNXMXLXNXMXJXDLMZBCNZXLXMXNXPXNEKZX
      DLMZBCNZUAXNUBXDLMZBCNZUAXNFKZXDLMZBCNZUAXNYBUBUCUDZXDLMZBCNZUAXNXPUAEFXJ
      XQUBPZXSYAXNYHXRXTBCXQUBXDLUEUFUGEFUHZXSYDXNYIXRYCBCXQYBXDLUEUFUGXQYEPZXS
      YGXNYJXRYFBCXQYEXDLUEUFUGEDUHZXSXPXNYKXRXOBCXQXJXDLUEUFUGXAYAXIXAXTBCXAXD
      CRZXDGRZXTCGXDUIZXDUJUKULUMYBGRZXNYDYGYOXAXIYDYGUAZYOXAXIYPYOXATZXITYDYGX
      IYDYBCRZQZYQYPYDXIYSYDYRXGYRYDXGYRYDXGXFYDAYBCAFUHXEYCBCXCYBXDLUEUFUNUOUP
      UQUPYQYSYPYQYSTYCYFBCYOXAYSYLYCYFUAZYOXAYLYSYTYOXAYLYSYTUAYOXAYLTZTZYCYSY
      FUVBYCYFQZYRUAYSYFUAUVBYCUVCYRUVBYCUVCTZFBUHZYRYOYMUVEUVDURUVAYOYMTZUVEYC
      XDYBLMZTZUVDYBUSRZXDUSRZUVEUVHURYOYMYBXDUTYBVAZXDVAZVBUVFUVGUVCYCUVFUVGXD
      YEVCMZUVCYMYOUVGUVMURXDYBVDVEUVIUVJUVMUVCURYOYMUVIUVJTYFUVMYEUSRZUVJYFUVM
      QURUVIYEXDVFUVIUBUSRUVNVGYBUBVHVIVJVKUVKUVLVBVLVMVLXAYLYMYNVNVOUVAUVEYRUA
      ZYOYLUVOXAXDCYBVPVQVQVRVSYFYRVTUKWAWBWCWDWEUOWFWGWHWIWJWKUPXIXPXLUAXAXPXI
      XLXPXKXGXKXPXGXKXPXGXFXPAXJCADUHXEXOBCXCXJXDLUEUFUNUOUPUQUPVQWLUPXMQZXAXL
      XIXAUVPXLXAXKXMCGXJUIUQUPWMWNUOWODCWPWQWRCIWSWTVN $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y z w v $.  $d w v z A $.
    nnwof.1 $e |- ( z e. A -> A. x z e. A ) $.
    nnwof.2 $e |- ( z e. A -> A. y z e. A ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element. $)
    nnwof $p |- ( ( A (_ NN /\ A =/= (/) ) -> E. x e. A A. y e. A x <_ y ) $=
      ( vw vv cn wss c0 wne wa cv cle wbr wral wrex nnwo wcel wi wal wex
      ax17el hbel ax-17 hbim weq eleq1 breq2 bi2imd cbv4 bilan biex hbal hban
      breq1 bilimd bialdv bi2and cbvex bitr df-rex 3bitr4 df-ral birex sylib )
      DIJDKLMGNZHNZOPZHDQZGDRZANZBNZOPZBDQZADRZGHDSVIDTZVJUAZHUBZGDRZVNDTZVOUAZ
      BUBZADRZVLVQVHDTZVTMZGUCZVMDTZWDMZAUCZWAWEWHWFWBVHVNOPZUAZBUBZMZGUCWKWGWO
      GVTWNWFVSWMHBVRVJBBCCVIDCHBUDFUEVJBUFUGWMHUFHBUHVRWBVJWLVIVNDUIVIVNVHOUJU
      KULUMUNWOWJGAWFWNAACCVHDCGAUDEUEWMABWBWLAACCVNDCBAUDEUEWLAUFUGUOUPWJGUFGA
      UHZWFWIWNWDVHVMDUIWPWMWCBWPWLVOWBVHVMVNOUQURUSUTVAVBVTGDVCWDADVCVDVKVTGDV
      JHDVEVFVPWDADVOBDVEVFVDVG $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y z $.  $d y z ph $.  $d x ps $.
    nnwos.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Well-ordering principle:  any non-empty set of natural numbers has a
       least element (schema form). $)
    nnwos $p |- ( E. x e. NN ph ->
                  E. x e. NN ( ph /\ A. y e. NN ( ps -> x <_ y ) ) ) $=
      ( vz cn crab wss c0 wne wa cv cle wbr wral wrex wi hbrab1 wcel ax-17
      nnwof wceq wn ssrab biantrur bicom df-ne bitr rnoel3 wex df-rex wal cab
      df-rab eleq2i abid df-ral rabcl birim impexp bial bi2an biex bilan anass
      3imtr3 ) ACGHZGIZVHJKZLZCMZDMZNOZDVHPZCVHQZACGQZABVNRZDGPZLZCGQZCDFVHACFG
      SFMVHTDUAUBVKVHJUCUDZVQVKVJWBVJVKVIVJACGUEUFUGVHJUHUIACGUJUIVPVLVHTZVOLZC
      UKZWAVOCVHULWEVLGTZALZVMGTZVRRZDUMZLZCUKZWAWDWKCWCWGVOWJWCVLWGCUNZTZWGVHW
      MVLACGUOUPWGWNWGCUQUGUIVOVMVHTZVNRZDUMWJVNDVHURWPWIDWPWHBLZVNRWIWOWQVNABC
      VMGEUSUTWHBVNVAUIVBUIVCVDWLWFVTLZCUKZWAWKWRCWKWGVSLZWRWTWKVSWJWGVRDGURVEU
      GWFAVSVFUIVDWAWSVTCGULUGUIUIUIVG $.
      $([17-Aug-01]$)
  $}

  ${
    $d x y $.  $d y ph $.  $d x ps $.
    $( Substitutions. $)
    indstr.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Induction hypothesis. $)
    indstr.2 $e |- ( x e. NN -> ( A. y e. NN ( y < x -> ps ) -> ph ) ) $.
    $( Strong Mathematical Induction for positive integers (inference schema). $)
    indstr $p |- ( x e. NN -> ph ) $=
      ( cn wral wn wrex cv cle wbr wi wa wcel pm3.24 clt cr wb leltt nnret
      syl2an bilimd pm4.1 syl6bbr biraldva sylbid imland ancom syl6ib mtoi
      rgen ralnex mpbi weq binegd nnwos mto dfral2 mpbir rspec ) ACGACGHAIZCGJZ
      IVDVCBIZCKZDKZLMZNZDGHZOZCGJZVKIZCGHVLIVMCGVFGPZVKAVCOZAQVNVKVCAOVOVNVJAV
      CVNVJVGVFRMZBNZDGHAVNVIVQDGVNVGGPZOZVIVEVPIZNVQVSVHVTVEVFSPVGSPVHVTTVNVRV
      FVGUAVFUBVGUBUCUDVPBUEUFUGFUHUIVCAUJUKULUMVKCGUNUOVCVECDCDUPABEUQURUSACGU
      TVAVB $.
      $([17-Aug-01]$)
  $}

  $( The reciprocal of a natural number is positive. $)
  nnrect $p |- ( A e. NN -> 0_10 < ( 1_10 / A ) ) $=
    ( cn wcel cn1 cle wbr cn0 cdiv co clt nnge1t wa cr wi nnret ax1re ax0re
    ltletrt mp3an1 mpan recgt0t syld syl lt01 jctl syl5 mpd ) ABCZDAEFZGDAHIJFZ
    AKUHGDJFZUILZUJUIUHAMCZULUJNAOUMULGAJFZUJDMCZUMULUNNZPGMCUOUMUPQGDARSTAUAUB
    UCUIUKUDUEUFUG $.
    $([25-Aug-99]$)

