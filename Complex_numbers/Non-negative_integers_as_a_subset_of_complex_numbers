$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Non-negative integers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the set of non-negative integers. $)
  df-n0  $a |- NN0 = ( NN u. { 0 } ) $.

  $( Nonnegative integers expressed in terms of naturals and zero.
     (Contributed by Raph Levien 10-Dec-02.) $)
  elnn0 $p |- ( A e. NN0 <-> ( A e. NN \/ A = 0 ) ) $=
    ( cn0 wcel cn cc0 csn cun wo wceq df-n0 eleq2i elun cc 0cn elisseti elsnc2
    bilor 3bitr ) ABCADEFZGZCADCZASCZHUAAEIZHBTAJKADSLUBUCUAAEEMNOPQR $.
    $([14-May-03]$)

  $( Natural number ordering relation generalized to non-negative integers.
     (Contributed by Raph Levien 10-Dec-02.) $)
  nn0ltle1 $p |- ( ( A e. NN0 /\ B e. NN0 ) ->
                ( A < B <-> ( A + 1 ) <_ B ) ) $=
    ( cn cc0 csn cun wcel clt wbr c1 caddc co cle wb cn0 nn0ltle1_ df-n0
    eleq2i syl2anb ) ACDEFZGBTGABHIAJKLBMINAOGBOGABPOTAQROTBQRS $.
    $([14-May-03]$)

  $( Positive naturals are a subset of nonnegative integers.  (Contributed
     by Raph Levien 10-Dec-02.) $)
  nnssnn0 $p |- NN (_ NN0 $=
    ( cn cc0 csn cun cn0 ssun1 df-n0 sseqtr4 ) AABCZDEAIFGH $.
    $([14-May-03]$)

  $( Nonnegative integers are a subset of the reals.  (Contributed by Raph
     Levien 10-Dec-02.) $)
  nn0ssre $p |- NN0 (_ RR $=
    ( cn0 cn cc0 csn cun cr df-n0 nnssre wcel wss ax0re snssi ax-mp unssi
    eqsstr ) ABCDZEFGBPFHCFIPFJKCFLMNO $.
    $([14-May-03]$)

  ${
    nn0re.1 $e |- A e. NN0 $.
    $( A non-negative integer is a real number. $)
    nn0re $p |- A e. RR $=
      ( cn0 cr nn0ssre ssii ) CDAEBF $.
      $([14-May-03]$)

    $( A non-negative integer is a complex number. $)
    nn0cn $p |- A e. CC $=
      ( nn0re recn ) AABCD $.
      $([14-May-03]$)
  $}

  $( 0 is a nonnegative integer.  (Contributed by Raph Levien 10-Dec-02.) $)
  0nn0 $p |- 0 e. NN0 $=
    ( cc0 cn0 wcel cn wceq wo cleqid olc ax-mp elnn0 mpbir ) ABCADCZAAEZFZMNAGM
    LHIAJK $.
    $([14-May-03]$)

  $( 1 is a nonnegative integer.  (Contributed by Raph Levien 10-Dec-02.) $)
  1nn0 $p |- 1 e. NN0 $=
    ( cn cn0 c1 nnssnn0 peano1c ssii ) ABCDEF $.
    $([14-May-03]$)

  $( 2 is a nonnegative integer.  (Contributed by Raph Levien 10-Dec-02.) $)
  2nn0 $p |- 2 e. NN0 $=
    ( cn cn0 c2 nnssnn0 2nn ssii ) ABCDEF $.
    $([14-May-03]$)

  $( Closure of addition of nonnegative integers.  (Contributed by Raph Levien
     10-Dec-02.)$)
  nn0addclt $p |- ( ( A e. NN0 /\ B e. NN0 ) -> ( A + B ) e. NN0 ) $=
    ( cn wcel cc0 wceq wo caddc co cn0 wa nnaddclt nnssnn0 ssi syl cc opreq1
    addid2t sylan9eq nncnt sylan5 adantl eqeltrd opreq2 ax0id sylan9eqr sylan
    adantr opreq12 0cn addid1 syl6eq 0nn0 a1i ccase elnn0 syl2anb ) ACDZAEFZGBC
    DZBEFZGABHIZJDZAJDZBJDZURUTUSVAVCURUTKVBCDVCABLCJVBMNOUSUTKVBBJUSBPDZVBBFUT
    USVFVBEBHIBAEBHQBRSBTUAUTVEUSCJBMNUBUCURVAKVBAJAPDZVAVBAFURVAVGVBAEHIABEAHU
    DAUEUFATUGURVDVACJAMNUHUCUSVAKZVBEJVHVBEEHIEAEBEHUIEUJUKULEJDVHUMUNUCUOAUPB
    UPUQ $.
    $([14-May-03]$)

  ${
    nn0addcl.1 $e |- A e. NN0 $.
    nn0addcl.2 $e |- B e. NN0 $.
    $( Closure of addition of nonnegative integers, inference form.  (Contributed
       by Raph Levien 10-Dec-02.) $)
    nn0addcl $p |- ( A + B ) e. NN0 $=
      ( cn0 wcel caddc co nn0addclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $([14-May-03]$)

    $( Closure of multiplication of nonnegative integers, inference form.
       (Contributed by Raph Levien 10-Dec-02.) $)
    nn0mulcl $p |- ( A x. B ) e. NN0 $=
      ( cn wcel cc0 wceq wo cmulc co cn0 elnn0 mpbi wa nnmulclt nnssnn0 ssi
      syl opreq1 nn0cn mulzer2 syl6eq 0nn0 a1i eqeltrd opreq2 mulzer1 ccase2
      mp2an ) AEFZAGHZIZBEFZBGHZIZABJKZLFZALFUMCAMNBLFUPDBMNUKUNULUOURUKUNOUQEF
      URABPELUQQRSULUQGLULUQGBJKGAGBJTBBDUAUBUCGLFZULUDUEUFUOUQGLUOUQAGJKGBGAJU
      GAACUAUHUCUSUOUDUEUFUIUJ $.
      $([14-May-03]$)
  $}

  ${
    $d x A $.
    nn0ge0i.1 $e |- A e. NN0 $.
    $( Non-negative integers are non-negative.  (Contributed by Raph Levien
       10-Dec-02.)$)
    nn0ge0i $p |- 0 <_ A $=
      ( cc0 cle wbr clt wceq wo cn wcel cn0 elnn0 mpbi nngt0t cleqcom biimp
      im2or ax-mp ax0re nn0re leloe mpbir ) CADECAFEZCAGZHZAIJZACGZHZUEAKJUHBAL
      MUFUCUGUDANUGUDACOPQRCASABTUAUB $.
      $([14-May-03]$)
  $}

  ${
    nn0le2sqet.1 $e |- A e. NN0 $.
    nn0le2sqet.2 $e |- B e. NN0 $.
    $( Two non-negative integers compare the same as their squares.
       (Contributed by Raph Levien 10-Dec-02.) $)
    nn0le2sqet $p |- ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) $=
      ( cle wbr c2 cexp co cmulc cc0 wb nn0ge0i nn0re le2sqe mp2an nn0cn sqval
      breq12i bitr ) ABEFZAGHIZBGHIZEFZAAJIZBBJIZEFKAEFKBEFUAUDLACMBDMABACNBDNO
      PUBUEUCUFEAACQRBBDQRST $.
      $([14-May-03]$)
  $}

  ${
    nn0addge1.1 $e |- A e. NN0 $.
    nn0addge1.2 $e |- B e. NN0 $.
    $( A non-negative integer is less than or equal to itself plus another.
       (Contributed by Raph Levien 10-Dec-02.) $)
    nn0addge1 $p |- A <_ ( A + B ) $=
      ( cc0 caddc co cle nn0cn addid1 wbr nn0ge0i ax0re nn0re leadd2 mpbi
      eqbrtrr ) AEFGZAABFGZHAACIJEBHKRSHKBDLEBAMBDNACNOPQ $.
      $([14-May-03]$)

    $( A non-negative integer is less than or equal to itself plus another.
      (Contributed by Raph Levien 10-Dec-02.) $)
    nn0addge2 $p |- B <_ ( A + B ) $=
      ( caddc co cle nn0addge1 nn0cn addcom breqtr ) BBAEFABEFGBADCHBABDIACIJK
      $.
      $([14-May-03]$)
  $}

  ${
    nn0le2x.1 $e |- A e. NN0 $.
    $( A non-negative integer is less than or equal to twice itself.
       (Contributed by Raph Levien 10-Dec-02.) $)
    nn0le2x $p |- A <_ ( 2 x. A ) $=
      ( caddc co c2 cmulc cle nn0addge1 nn0cn 2times breqtrr ) AAACDEAFDGAABBHA
      ABIJK $.
      $([14-May-03]$)
  $}

  ${
    nn0lele2x.1 $e |- A e. NN0 $.
    nn0lele2x.2 $e |- B e. NN0 $.
    $( 'Less than or equal to' implies 'less than or equal to twice' for
       non-negative integers.  (Contributed by Raph Levien 10-Dec-02.) $)
    nn0lele2x $p |- ( B <_ A -> B <_ ( 2 x. A ) ) $=
      ( cle wbr c2 cmulc co nn0le2x nn0re 2re mulrcl letr mpan2 ) BAEFAGAHIZEFB
      PEFACJBAPBDKACKZGALQMNO $.
      $([14-May-03]$)
  $}

  ${
    nn0opthlem1.1 $e |- A e. NN0 $.
    nn0opthlem1.2 $e |- C e. NN0 $.
    $( A rather pretty lemma for ~ nn0opth .  (Contributed by Raph Levien
      10-Dec-02.) $)
    nn0opthlem1  $p |- ( A < C <-> ( ( A x. A ) + ( 2 x. A ) ) < ( C x. C ) ) $=
      ( c1 caddc co cle wbr cmulc clt c2 1nn0 nn0addcl nn0le2sqet cn0 wcel wb
      nn0ltle1 mp2an nn0mulcl 2nn0 cexp nn0cn 1cn binom addcl sqval opreq1i
      opreq12i 3eqtr3 mulid1 opreq2i eqtr breq1i bitr4 3bitr4 ) AEFGZBHIZURURJG
      ZBBJGZHIZABKIZAAJGZLAJGZFGZVAKIZURBAECMNDOAPQBPQVCUSRCDABSTVGVFEFGZVAHIZV
      BVFPQVAPQVGVIRVDVEAACCUALAUBCUANBBDDUAVFVASTUTVHVAHUTVDLAEJGZJGZFGZEEJGZF
      GZVHURLUCGALUCGZVKFGZELUCGZFGUTVNAEACUDZUEUFURAEVRUEUGUHVPVLVQVMFVOVDVKFA
      VRUHUIEUEUHUJUKVLVFVMEFVKVEVDFVJALJAVRULUMUMEUEULUJUNUOUPUQ $.
      $([14-May-03]$)
  $}

  ${
    nn0opth.1 $e |- A e. NN0 $.
    nn0opth.2 $e |- B e. NN0 $.
    nn0opth.3 $e |- C e. NN0 $.
    nn0opth.4 $e |- D e. NN0 $.
    $( Lemma for ~ nn0opth .  (Contributed by Raph Levien 10-Dec-02.) $)
    nn0opthlem2 $p |- ( ( B <_ A /\ D <_ C ) ->
              ( A < C -> -. ( ( A x. A ) + B ) = ( ( C x. C ) + D ) ) ) $=
      ( cle wbr wa clt cmulc co caddc wceq wn wi c2 nn0re mulrcl addrcl 2re
      lelttr nn0lele2x leadd2 sylib nn0opthlem1 biimp syl2an cc0 recn addid1
      nn0ge0i ax0re mpbi eqbrtrr ltletr mpan2 syl exp adantr ltne syl6 ) BAIJZD
      CIJZKACLJZAAMNZBONZCCMNZDONZLJZVIVKPQVEVGVLRVFVEVGVLVEVGKVIVJLJZVLVIVHSAM
      NZONZIJZVOVJLJZVMVEVGVIVOVJVHBAAAETZVRUAZBFTZUBZVHVNVSSAUCVRUAZUBCCCGTZWC
      UAZUDVEBVNIJVPABEFUEBVNVHVTWBVSUFUGVGVQACEGUHUIUJVMVJVKIJVLVJUKONZVJVKIVJ
      VJWDULUMUKDIJWEVKIJDHUNUKDVJUODHTZWDUFUPUQVIVJVKWAWDVJDWDWFUBZURUSUTVAVBV
      IVKWAWGVCVD $.
      $([14-May-03]$)

    $( An ordered pair theorem for non-negative integers.  Theorem 17.3 of
       [Quine] p. 124.  We can represent an ordered pair of non-negative
       integers ` A ` and ` B ` by ` ( ( ( A + B ) x. ( A + B ) ) + B ) ` . If
       two such ordered pairs are equal, their first elements are equal and
       their second elements are equal.  Contrast this ordered pair
       representation with the standard one ~ df-op that works for any set.
       (Contributed by Raph Levien 10-Dec-02.)  $)
    nn0opth $p |- ( ( ( ( A + B ) x. ( A + B ) ) + B ) =
            ( ( ( C + D ) x. ( C + D ) ) + D ) <-> ( A = C /\ B = D ) ) $=
      ( caddc co cmulc wceq wa cle wbr wi nn0addge2 clt wo wn nn0addcl
      nn0opthlem2 ancoms cleqcom bineg syl6ibr jaod nn0re addrcl lttri2 syl5ib
      a3d mp2an id opreq12d opreq1d eqtr4d nn0cn addcl mulcl addcan sylib
      opreq2d addcan2 jca opreq12 pm3.27 impbi ) ABIJZVIKJZBIJZCDIJZVLKJZDIJZLZ
      ACLZBDLZMZVOVPVQVOVICBIJZLVPVOVIVLVSBVINOZDVLNOZVOVIVLLZPABEFQCDGHQVTWAMZ
      WBVOWCVIVLROZVLVIROZSVOTZWBTWCWDWFWEVIBVLDABEFUAZFCDGHUAZHUBWCWEVNVKLZTZW
      FWAVTWEWJPVLDVIBWHHWGFUBUCVOWIVKVNUDUEUFUGVIVLABAEUHBFUHUIVLWHUHUJUKULUMZ
      VOBDCIVOVKVJDIJZLVQVOVKVNWLVOUNVOVJVMDIVOVIVLVIVLKWKWKUOUPUQVJBDVIVIABAEU
      RZBFURZUSZWOUTWNDHURVAVBZVCUQACBWMCGURWNVDVBWPVEVRVJVMBDIVRVIVLVIVLKACBDI
      VFZWQUOVPVQVGUOVH $.
      $([14-May-03]$)
  $}

  $( No number less than zero is a non-negative integer. $)
  lt0nnn0 $p |- ( ( A e. RR /\ A < 0 ) -> -. A e. NN0 ) $=
    ( cr wcel cc0 clt wbr cn0 wn cn wceq wo c1 lt01 wa wi ax1re ax0re axlttrn
    mp3an2 mpan2 mpan2i lt1nnn exp syld pm2.03d breq2 binegd ltnrt syl5bi comm
    jaod elnn0 syl5ib imp ) ABCZADEFZAGCZHUOUQUPUOAICZADJZKUPHZUQUOURUTUSUOUPUR
    UOUPALEFZURHZUOUPDLEFZVAMUOLBCZUPVCNVAOZPUODBCVDVEQADLRSTUAUOVAVBAUBUCUDUEU
    SUOUTUSAAEFZHUTUOUSVFUPADAEUFUGAUHUIUJUKAULUMUEUN $.
    $([?]$)

