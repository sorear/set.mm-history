$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Some properties of specific numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( The square of 1 is 1. $)
  sq1 $p |- ( 1 ^ 2 ) = 1 $=
    ( c1 c2 cexp co cmulc 1cn sqval mulid1 eqtr ) ABCDAAEDAAFGAFHI $.
    $([22-Aug-99]$)

  $( The square of 2 is 4. $)
  sq2 $p |- ( 2 ^ 2 ) = 4 $=
    ( c2 cexp co cmulc c4 2re recn sqval 2t2e4 eqtr ) AABCAADCEAAFGHIJ $.
    $([22-Aug-99]$)

  $( The square root of 1 is 1. $)
  sqr1 $p |- ( sqr ` 1 ) = 1 $=
    ( c1 cmulc co csqr cfv 1cn mulid1 fveq2i cc0 cle wbr wceq ax0re ax1re lt01
    ltlei sqrsqid ax-mp eqtr3 ) AABCZDEZADEATADAFGHIAJKUAALIAMNOPANQRS $.
    $([31-Jul-99]$)

  $( The square root of 4 is 2. $)
  sqr4 $p |- ( sqr ` 4 ) = 2 $=
    ( c4 csqr cfv c2 wceq cmulc co 2t2e4 cleqcomi cc0 clt wbr wb 4pos 2pos cle
    4re 2re sqrsq ax0re ltle syl2an mp2an mpbir ) ABCDEZADDFGZEZUFAHIJAKLZJDKLZ
    UEUGMZNOJAPLJDPLUJUHUIADQRSJATQUAJDTRUAUBUCUD $.
    $([3-Aug-99]$)

  $( The square root of 9 is 3. $)
  sqr9 $p |- ( sqr ` 9 ) = 3 $=
    ( c9 csqr cfv c3 wceq cmulc co 3t3e9 cleqcomi cc0 clt wbr wb 9pos 3pos cle
    9re 3re sqrsq ax0re ltle syl2an mp2an mpbir ) ABCDEZADDFGZEZUFAHIJAKLZJDKLZ
    UEUGMZNOJAPLJDPLUJUHUIADQRSJATQUAJDTRUAUBUCUD $.
    $([?]$)

  ${
    binom.1 $e |- A e. CC $.
    binom.2 $e |- B e. CC $.
    $( Binomial expansion. $)
    binom $p |- ( ( A + B ) ^ 2 ) = ( ( ( A ^ 2 ) + ( 2 x.
                ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( caddc co cmulc c2 cexp addcl distr1 distr2 mulcom opreq2i eqtr
      opreq12i mulcl addass opreq1i eqtr3 3eqtr sqval 2times 3eqtr4 ) ABEFZUEGF
      ZAAGFZABGFZUHEFZEFZBBGFZEFZUEHIFAHIFZHUHGFZEFZBHIFZEFUFUEAGFZUEBGFZEFUGUH
      EFZUHUKEFZEFZULUEABABCDJZCDKUQUSURUTEUQUGBAGFZEFUSABACDCLVCUHUGEBADCMNOAB
      BCDDLPUSUHEFZUKEFVAULUSUHUKUGUHAACCQZABCDQZJVFBBDDQRVDUJUKEUGUHUHVEVFVFRS
      TUAUEVBUBUOUJUPUKEUMUGUNUIEACUBUHVFUCPBDUBPUD $.
      $([11-Aug-99]$)
  $}

  ${
    discrlem.1 $e |- A e. RR $.
    discrlem.2 $e |- B e. RR $.
    discrlem.3 $e |- C e. RR $.
    ${
      discrlem1.4 $e |- D = -u ( B / ( 2 x. A ) ) $.
      ${
        discrlem1.5 $e |- 0 < A $.
        $( Lemma for discriminant theorem. $)
        discrlem1 $p |- ( 0 <_
                     ( ( ( A x. ( D ^ 2 ) ) + ( B x. D ) ) + C ) <->
                     ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) <_ 0 ) $=
          ( cc0 c2 cexp co cmulc caddc cle wbr cneg c4 cmin 4re recn mulcl
          mulzer1 cdiv cr 2re mulrcl 2pos gt0ne0i muln0 divrcl negrcl eqeltr
          sqcl cc divcl negcl addcl distr1 breq12i clt wb 4pos mulgt0i ax0re
          sqrecl addrcl lemul2 ax-mp neg0 negdist2 c1 divcan3 opreq1i sqval
          mul2neg eqtr eqtr2 opreq12i mulass divmul13 divid opreq2i 1cn mulcom
          3eqtr eqtr3 divcan2 negneg mulneg1 df-2 negeqi negdist negid addass
          addid2 3eqtr3 distr2 mulm1 2t2e4 divmuldiv eqtr4 mulneg2 3bitr4
          subrcl leneg bitr4 ) JADKLMZNMZBDNMZOMZCOMZPQZJRZBKLMZSACNMZNMZTMZRZP
          QZXSJPQSANMZJNMZYBXMNMZPQZJYBXLNMZYBCNMZOMZPQXNYAYCJYDYHPYBSASUAUBZAE
          UBZUCZUDYBXLCYKXJXKAXIYJDDDBKANMZUEMZRZUFHYMBYLFKAUGEUHZKAKUGUBZYJKUG
          UIUJZAEIUJZUKZULUMZUNZUBUOUCBDBFUBZDYNUPHYMBYLUVBYLYOUBZYSUQZURUNUCUS
          CGUBZUTVAJYBVBQXNYEVCSAUAEVDIVEJXMYBVFXLCXJXKAXIEDUVAVGUHBDFUVAUHVHGV
          HSAUAEUHZVIVJXOJXTYHPVKXTXPRZXROMYHXPXRXPBFVGZUBZSXQYIACYJUVEUCUCVLYF
          UVGYGXROYFYBXPYBUEMZRZNMYBUVJNMZRUVGXLUVKYBNXLVMBKUEMZNMZYMNMZKRZUVMN
          MZYMNMZOMZUVKXJUVOXKUVROYLKUEMZYMYMNMZNMZXJUVOUVTAUWAXINKAYPYJYQVNXIY
          NKLMZUWADYNKLHVOUWCYNYNNMUWAYNYNYTUBVPYMYMUVDUVDVQVRVSVTUVTYMNMZYMNMU
          WBUVOUVTYMYMYLKUVCYPYQUQUVDUVDWAUWDUVNYMNUWDUVMYLYLUEMZNMUVMVMNMUVNYL
          KBYLUVCYPUVBUVCYQYSWBUWEVMUVMNYLUVCYSWCWDUVMVMBKUVBYPYQUQZWEWFWGVOWHW
          HXKUVQRZYNNMUVRBUWGDYNNKUVMNMZBUWGKBYPUVBYQWIUVPRZUVMNMUWHUWGUWIKUVMN
          KYPWJVOUVPUVMKYPURZUWFWKWHWHHVTUVQYMUVPUVMUWJUWFUCZUVDVQVRVTUVNUVQOMZ
          YMNMUVMRZYMNMZUVSUVKUWLUWMYMNVMUVPOMZUVMNMVMRZUVMNMUWLUWMUWOUWPUVMNUW
          OVMUWPUWPOMZOMZUWPUVPUWQVMOUVPVMVMOMZRUWQKUWSWLWMVMVMWEWEWNVRWDVMUWPO
          MZUWPOMJUWPOMUWRUWPUWTJUWPOVMWEWOVOVMUWPUWPWEVMWEURZUXAWPUWPUXAWQWRVR
          VOVMUVPUVMWEUWJUWFWSUVMUWFWTWRVOUVNUVQYMVMUVMWEUWFUCUWKUVDWSUWNUVMYMN
          MZRUVKUVMYMUWFUVDWKUVJUXBUVJBBNMZKYLNMZUEMUXBXPUXCYBUXDUEBUVBVPKKNMZA
          NMYBUXDUXESANXAVOKKAYPYPYJWAWHVTBKBYLUVBYPUVBUVCYQYSXBXCWMXCWRVRWDYBU
          VJYBUVFUBZUVJXPYBUVHUVFSAYIYJSUAVDUJYRUKZULUBXDUVLXPYBXPUXFUVIUXGWIWM
          WGSACYIYJUVEWAVTXCVAXEXSJXPXRUVHSXQUAACEGUHUHXFVFXGXH $.
          $([6-Aug-99]$)
      $}

      ${
        discrlem2.5 $e |- ( D e. RR -> 0 <_
                     ( ( ( A x. ( D ^ 2 ) ) + ( B x. D ) ) + C ) ) $.
        $( Lemma for discriminant theorem. $)
        discrlem2 $p |- ( 0 < A ->
                      ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) <_ 0 ) $=
          ( cc0 clt wbr c2 cexp co cmulc caddc cle c4 cmin cr wcel cdiv cneg
          wne 2pos 2re mulgt0 mpan mulrcl gt0ne0 syl divrclz negrclt 3syl
          eleq1i sylibr wb c1 cded wceq id opreq2 opreq2d negeqd syl5eq
          opreq1d opreq12d breq2d opreq1 breq1d bi2bid ax1re keepel cleqid
          elimgt0 discrlem1 dedth mpbid ) JAKLZJADMNOZPOZBDPOZQOZCQOZRLZBMNOZSA
          CPOZPOZTOZJRLZVTDUAUBZWFVTBMAPOZUCOZUDZUAUBZWLVTWMJUEZWNUAUBWPVTJWMKL
          ZWQJMKLVTWRUFMAUGEUHUIWMMAUGEUJZUKULBWMFWSUMWNUNUODWOUAHUPUQIULVTWFWK
          URJVTAUSUTZBMWTPOZUCOZUDZMNOZPOZBXCPOZQOZCQOZRLZWGSWTCPOZPOZTOZJRLZUR
          AUSAWTVAZWFXIWKXMXNWEXHJRXNWDXGCQXNWBXEWCXFQXNAWTWAXDPXNVBXNDXCMNXNWO
          XCDXNWNXBXNWMXABUCAWTMPVCVDVEHVFZVGVHXNDXCBPXOVDVHVGVIXNWJXLJRXNWIXKW
          GTXNWHXJSPAWTCPVJVDVDVKVLWTBCXCVTAUSUAEVMVNFGXCVOAVPVQVRVS $.
          $([9-Aug-99]$)
      $}

    $}

    ${
      discrlem3.4 $e |- D = ( ( C + 1 ) / -u B ) $.
      discrlem3.5 $e |- ( D e. RR -> 0 <_
                     ( ( ( A x. ( D ^ 2 ) ) + ( B x. D ) ) + C ) ) $.
      $( Lemma for discriminant theorem. $)
      discrlem3 $p |- ( 0 = A ->
                    ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) <_ 0 ) $=
        ( cc0 wceq c2 cexp co c4 cmulc cmin cle wbr wn c1 caddc clt ltplus1
        cneg wne wi df-ne recn negne0 bitr3 wa cr wcel cdiv ax1re addrcl
        negrcl divrclz eleq1i sylibr syl adantr opreq1 cleqcomd cc recnd sqclt
        mulzer2t 3syl sylan9eqr opreq1d jctil axmulrcl addid2t eqtrd breqtrd
        wb ax0re lesubadd2t mp3an3 mpan mpbird recnt im2an mulneg1t df-neg
        opreq2i 3eqtr3g negcl divcan2z breq1d mpbid lelt sylib exp sylbi mt2i
        a3i mulzer2 syl6eq sqval syl5eq syl5reqr opreq2d 4re mulzer1 opreq12d
        0cn subid sqrecl mulrcl subrcl eqle ) JAKZBLMNZOACPNZPNZQNZJKXSJRSXOXSJ
        JQNJXOXPJXRJQXOBBPNZJXPXOXTJBPNJXOBJBPBJKZXOYATZXOCCUAUBNZUCSZCGUDYBBUE
        ZJUFZXOYDTZUGYBBJUFYFBJUHBBFUIZUJUKYFXOYGYFXOULZYCCRSZYGYIJBDPNZQNZCRSZ
        YJYIYMJYKCUBNZRSZYIJADLMNZPNZYKUBNZCUBNZYNRYFJYSRSZXOYFDUMUNZYTYFYCYEUO
        NZUMUNUVAYCYECUAGUPUQZBFURUSDUVBUMHUTVAZIVBVCYIYRYKCUBYIYRJYKUBNZYKYIYQ
        JYKUBXOYFYQJYPPNZJXOUVFYQJAYPPVDVEYFDVFUNZYPVFUNUVFJKYFDUVDVGDVHYPVIVJV
        KVLYFUVEYKKZXOYFYKVFUNUVHYFYKYFBUMUNZUVAULZYKUMUNZYFUVAUVIUVDFVMZBDVNZV
        BVGYKVOVBVCVPVLVQYFYMYOVRZXOYFUVJUVKUVNUVLUVMJUMUNZUVKUVNVSUVOUVKCUMUNU
        VNGJYKCVTWAWBVJVCWCYFYMYJVRXOYFYLYCCRYFYLYEUVBPNZYCYFYKUEZYEDPNZYLUVPYF
        UVRUVQYFUVJBVFUNZUVGULUVRUVQKUVLUVIUVSUVAUVGBWDDWDWEBDWFVJVEYKWGDUVBYEP
        HWHWIYEYCBYHWJYCUVCUIWKVPWLVCWMYCCUVCGWNWOWPWQWRWSVLBYHWTXABYHXBXCXOXRO
        JPNJXOXQJOPXOJCPNXQJJACPVDCCGUIWTXDXEOOXFUIXGXAXHJXIXJXAXSJXPXRBFXKOXQX
        FACEGXLXLXMVSXNVB $.
        $([9-Aug-99]$)

    $}

    ${
      $d x A $.  $d x B $.  $d x C $.
      discrlem.4 $e |- A. x e. RR 0 <_
                     ( ( ( A x. ( x ^ 2 ) ) + ( B x. x ) ) + C ) $.
      $( If a quadratic polynomial (with a non-negative high-order coefficient)
         is non-negative for all values, then its discriminant is
         non-positive. $)
      discrlem $p |- ( 0 <_ A ->
                      ( ( B ^ 2 ) - ( 4 x. ( A x. C ) ) ) <_ 0 ) $=
        ( cc0 cle wbr clt wceq wo c2 cexp co c4 cmulc cmin ax0re leloe cdiv
        cneg cleqid cv caddc cr opreq1 opreq2d opreq2 opreq12d opreq1d breq2d
        vtoclri discrlem2 c1 discrlem3 jaoi sylbi ) IBJKIBLKZIBMZNCOPQRBDSQSQTQ
        IJKZIBUAEUBVAVCVBBCDCOBSQUCQUDZEFGVDUEIBAUFZOPQZSQZCVESQZUGQZDUGQZJKZIB
        VDOPQZSQZCVDSQZUGQZDUGQZJKAVDUHVEVDMZVJVPIJVQVIVODUGVQVGVMVHVNUGVQVFVLB
        SVEVDOPUIUJVEVDCSUKULUMUNHUOUPBCDDUQUGQCUDUCQZEFGVRUEVKIBVROPQZSQZCVRSQ
        ZUGQZDUGQZJKAVRUHVEVRMZVJWCIJWDVIWBDUGWDVGVTVHWAUGWDVFVSBSVEVROPUIUJVEV
        RCSUKULUMUNHUOURUSUT $.
        $([10-Aug-99]$)
    $}
  $}

