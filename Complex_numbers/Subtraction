$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define subtraction. $)
    df-sub $a |- - = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. CC ) /\
                   z = U. { w e. CC | ( y + w ) = x } ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    subval.1 $e |- A e. CC $.
    subval.2 $e |- B e. CC $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A `. $)
    subval $p |- ( A - B ) = U. { x e. CC | ( B + x ) = A } $=
      ( vy vz vw cc wcel cmin co cv caddc wceq crab cuni axcnex zfausrab zfun2
      cleq2 rbiabdv unieqd opreq1 cleq1d df-sub oprabval2 mp2an ) BIJCIJBCKLCAM
      ZNLZBOZAIPZQZOFGHBCIIGMZUINLZFMZOZAIPZQUMKUOBOZAIPZQULUKAIRSTUPBOZURUTVAU
      QUSAIUPBUOUAUBUCUNCOZUTULVBUSUKAIVBUOUJBUNCUINUDUEUBUCFGHAUFUGDEUH $.
      $([8-May-99]$)
  $}

  $( Define the negative of a number (unary minus). $)
  df-neg $a |- -u A = ( 0 _10 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cn0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $([10-Feb-95]$)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi   $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $([14-Feb-95]$)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd   $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $([14-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcl   $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crab cuni subval wreu wcel negeux reucl
      ax-mp eqeltr ) ABFGBEHIGAJZEKLMZKEABCDNUAEKOUBKPEBADCQUAEKRST $.
      $([26-Nov-94]$)
  $}

  $( Closure law for subtraction. $)
  subclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cn0 cded wceq opreq1 eleq1d opreq2 0cn elimel subcl
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $([10-May-99]$)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negation. $)
    negcl   $p |- -u A e. CC $=
      ( cneg cn0 cmin co cc df-neg 0cn subcl eqeltr ) ACDAEFGAHDAIBJK $.
      $([26-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subadd   $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin co wceq caddc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu reuuni negeux mpan2 cleqcom subval cleq2i bitr syl6rbbr
      vtoclga ax-mp ) CHIABJKZCLZBCMKZALZNZFUMGOZLZBURMKZALZNUQGCHURCLZUSUNVAUP
      URCUMPVBUTUOAURCBMQRSURHIZVAURVAGHTUAZLZUSVCVAGHUBVAVENVAGHUCGBAEDUDUEUSU
      RUMLVEUMURUFUMVDURGABDEUGUHUIUJUKUL $.
      $([26-Nov-94]$)
  $}

  $( Relationship between subtraction and addition. $)
  subaddt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cn0 cded opreq1 cleq1d cleq2 bi2bid opreq2
    0cn elimel subadd dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUIH
    ZJABCKKKUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJAUIHZUFUKUHULVBUEUJC
    AUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACURU
    NOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $([20-Jan-97]$)

  ${
    subaddeq.1 $e |- A e. CC $.
    subaddeq.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    subaddeq   $p |- ( A + ( B - A ) ) = B $=
      ( cmin co wceq caddc subcl subadd cleqid mpbi ) BAEFZMGAMHFBGBAMDCBADCIJM
      KL $.
      $([26-Nov-94]$)
  $}

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negid   $p |- ( A + -u A ) = 0 _10 $=
      ( cneg caddc co cn0 cmin df-neg opreq2i 0cn subaddeq eqtr ) AACZDEAFAGEZD
      EFMNADAHIAFBJKL $.
      $([26-Nov-94]$)
  $}

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of
       Apostol, p. 18. $)
    subneg   $p |- ( A - B ) = ( A + -u B ) $=
      ( cmin co cneg caddc wceq negcl addcl subadd cn0 0cn negid addcoml
      opreq2i addid1 eqtr addass1l mpbir ) ABEFABGZHFZIBUCHFAIABUCCDAUBCBDJZKZL
      UCBAUEDCAUBBACUDDCAUBBHFZHFAMHFAUFMAHBUBMDUDNBDOPQACRSTPUA $.
      $([26-Nov-94]$)
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of
     Apostol, p. 18. $)
  subnegt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) = ( A + -u B ) ) $=
    ( cc wcel cmin co cneg caddc wceq cn0 cded opreq1 cleq12d opreq2 negeq
    opreq2d 0cn elimel subneg dedth2h ) ACDZBCDZABEFZABGZHFZIUAAJKZBEFZUFUDHFZI
    UFUBBJKZEFZUFUIGZHFZIABJJAUFIUCUGUEUHAUFBELAUFUDHLMBUIIZUGUJUHULBUIUFENUMUD
    UKUFHBUIOPMUFUIAJCQRBJCQRST $.
    $([21-Jan-97]$)

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       Apostol, p. 18. $)
    negneg   $p |- -u -u A = A $=
      ( cneg caddc co cn0 negcl negid opreq2i opreq1i addid2 eqtr addass2l
      addid1 3eqtr3 ) AACZPCZDEZDEAFDEQARFADPABGZHIAPQQBSPSGZTAPDEZQDEFQDEQUAFQ
      DABHJQTKLMABNO $.
      $([8-Feb-95]$)

    $( Subtraction of a number from itself. $)
    subid   $p |- ( A - A ) = 0 _10 $=
      ( cmin co cneg caddc cn0 subneg negid eqtr ) AACDAAEFDGAABBHABIJ $.
      $([26-Nov-94]$)

    $( Identity law for subtraction. $)
    subid1 $p |- ( A - 0 _10 ) = A $=
      ( cn0 cmin co wceq caddc 0cn subadd addid2 mpbir ) ACDEAFCAGEAFACABHBIABJ
      K $.
      $([29-May-99]$)

  $}

  ${
    subeq0.1 $e |- A e. CC $.
    subeq0.2 $e |- B e. CC $.
    $( If the difference between two numbers is zero, they are equal. $)
    subeq0 $p |- ( ( A - B ) = 0 _10 <-> A = B ) $=
      ( cmin co cn0 wceq cneg caddc subneg cleq1i opreq1 sylbi negcl add23
      addass negid opreq2i addid1 eqtr 3eqtr addid2 3eqtr3g subid cleq2i biimp
      syl impbi ) ABEFZGHZABHZUKABIZJFZBJFZGBJFZABUKUNGHUOUPHUJUNGABCDKLUNGBJMN
      UOABJFUMJFABUMJFZJFZAAUMBCBDOZDPABUMCDUSQURAGJFAUQGAJBDRSACTUAUBBDUCUDULU
      JBBEFZHZUKABBEMVAUKUTGUJBDUEUFUGUHUI $.
      $([8-May-99]$)

    $( Negative is one-to-one. $)
    neg11 $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cn0 cmin co caddc df-neg cleq12i 0cn subcl subadd subneg
      opreq2i eqtr2 cleq1i subeq0 bitr 3bitr ) AEZBEZFGAHIZGBHIZFAUFJIZGFZABFZU
      CUEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGUKAUDJIUGABCDPUDUFAJUJQRSABCDTUA
      UB $.
      $([1-Aug-99]$)
  $}

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0 _10 = 0 _10 $=
    ( cn0 cneg cmin co df-neg 0cn subid eqtr ) ABAACDAAEAFGH $.
    $([17-Jan-97]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    negrcl.1 $e |- A e. RR $.
    $( Closure law for negation of reals. $)
    negrcl   $p |- -u A e. RR $=
      ( vx cv cr wcel caddc co cn0 wceq wa wex cneg wrex df-rex axrnegex ax-mp
      mpbi cmin cc wb recnt cded cleq2 opreq2 cleq1d bi2bid 0cn recn elimel
      subadd dedth syl df-neg cleq1i syl5bb eleq1a sylbird imp 19.23aiv ) CDZEF
      ZAVAGHZIJZKZCLZAMZEFZVDCENZVFVDCEOAEFVIBCAPQRVEVHCVBVDVHVBVDVGVAJZVHVBIAS
      HZVAJZVDVJVBVATFZVLVDUAZVAUBVMVNVKVMVAIUCZJZAVOGHZIJZUAVAIVAVOJZVLVPVDVRV
      AVOVKUDVSVCVQIVAVOAGUEUFUGIAVOUHABUIVAITUHUJUKULUMVGVKVAAUNUOUPVAEVGUQURU
      SUTQ $.
      $([17-Jan-97]$)
  $}

  $( Closure law for negation of reals. $)
  negrclt $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg cn0 cded wceq negeq eleq1d ax0re elimel negrcl dedth ) ABCZA
    DZBCNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $([20-Jan-97]$)

  $( Closure law for subtraction of reals. $)
  subrclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cmin co cneg caddc cc wceq subnegt recnt syl2an eleq1d
    axaddrcl negrclt sylan5 mpbird ) ACDZBCDZEZABFGZCDABHZIGZCDZUBUCUECAJDBJDUC
    UEKTUAABLAMBMNOTUDCDUFUAAUDPBQRS $.
    $([20-Jan-97]$)

  ${
    subrcl.1 $e |- A e. RR $.
    subrcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    subrcl   $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co subrclt mp2an ) AEFBEFABGHEFABICDJ $.
      $([17-Jan-97]$)
  $}

