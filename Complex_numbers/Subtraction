$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.
    $( Define subtraction, the (unique) element ` x ` such that
       ` B + x = A `. $)
    df-sub $a |- ( A - B ) = U. { x e. CC | ( B + x ) = A } $.
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Subtraction exists. $)
    subex $p |- ( A - B ) e. V $=
      ( vx cmin cvv wcel cv caddc co wceq cc crab cuni df-sub cleq3i axcnex
      zfausrab zfun2 mpbir ) ABDZEFBCGHIAJZCKLZMZEFTUCECABNOUBUACKPQRS $.
      $([21-Jan-97]$)
  $}

  $( Define the negative of a number. $)
  df-neg $a |- ( - A ) = ( 0 _10 - A ) $.

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Equality theorem for subtraction. $)
    subeq1 $p |- ( A = B -> ( A - C ) = ( B - C ) ) $=
      ( vx wceq cv caddc co cc crab cuni cmin cleq2 rbiabdv unieqd df-sub
      3eqtr4g ) ABEZCDFGHZAEZDIJZKSBEZDIJZKACLBCLRUAUCRTUBDIABSMNODACPDBCPQ $.
      $([10-Feb-95]$)

    $( Equality theorem for subtraction. $)
    subeq2 $p |- ( A = B -> ( C - A ) = ( C - B ) ) $=
      ( vx wceq cv caddc co cc crab cuni cmin opreq1 cleq1d rbiabdv unieqd
      df-sub 3eqtr4g ) ABEZADFZGHZCEZDIJZKBTGHZCEZDIJZKCALCBLSUCUFSUBUEDISUAUDC
      ABTGMNOPDCAQDCBQR $.
      $([10-Feb-95]$)
  $}

  ${
    subeqi.1 $e |- A = B $.

    $( Equality inference for subtraction. $)
    subeq1i $p |- ( A - C ) = ( B - C ) $=
      ( wceq cmin subeq1 ax-mp ) ABEACFBCFEABCGDH $.
      $([15-Feb-95]$)

    $( Equality inference for subtraction. $)
    subeq2i $p |- ( C - A ) = ( C - B ) $=
      ( wceq cmin subeq2 ax-mp ) ABECAFCBFEABCGDH $.
      $([15-Feb-95]$)

    ${
      subeqi.2 $e |- C = D $.

      $( Equality inference for subtraction. $)
      subeq12i $p |- ( A - C ) = ( B - D ) $=
      ( cmin subeq1i subeq2i eqtr ) ACGBCGBDGABCEHCDBFIJ $.
      $([22-Feb-95]$)
    $}
  $}

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> ( - A ) = ( - B ) ) $=
      ( wceq cn0 cmin cneg subeq2 df-neg 3eqtr4g ) ABCDAEDBEAFBFABDGAHBHI $.
      $([10-Feb-95]$)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi   $p |- ( - A ) = ( - B ) $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDABFCG $.
      $([14-Feb-95]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcl   $p |- ( A - B ) e. CC $=
      ( vx cmin cv caddc co wceq cc crab cuni df-sub wreu wcel reucl negeux
      ax-mp eqeltr ) ABFBEGHIAJZEKLMZKEABNUAEKOUBKPUAEKQEBADCRST $.
      $([26-Nov-94]$)
  $}

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negation. $)
    negcl   $p |- ( - A ) e. CC $=
      ( cneg cn0 cmin cc df-neg 0cn subcl eqeltr ) ACDAEFAGDAHBIJ $.
      $([26-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subadd   $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin wceq caddc co wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu reuuni negeux mpan2 cleqcom df-sub cleq2i bitr syl6rbbr
      vtoclga ax-mp ) CHIABJZCKZBCLMZAKZNZUMGOZKZBURLMZAKZNUQGCHURCKZUSUNVAUPUR
      CUMPVBUTUOAURCBLQRSURHIZVAURVAGHTUAZKZUSVCVAGHUBVAVENVAGHUCGBAEDUDUEUSURU
      MKVEUMURUFUMVDURGABUGUHUIUJUKFUL $.
      $([26-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    $( Relationship between subtraction and addition. $)
    subaddt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
      ( vx cmin wceq caddc co wb cv cn0 wcel cc wi wa cab subeq1 cleq1d cleq2
      bi2bid subeq2 opreq1 opreq2 0cn cldedg2 subadd cldedg33 ) ABEZCFZBCGHZAF
      ZIDJZKLZAMLZNULALUNONDPZBEZCFZUJUOFZIUOUMBMLZNULBLUSONDPZEZCFZUTCGHZUOFZI
      VAUMCMLZNULCLVEONDPZFZUTVFGHZUOFZIDABCKMAUOFZUIUQUKURVJUHUPCAUOBQRAUOUJST
      BUTFZUQVBURVDVKUPVACBUTUOUARVKUJVCUOBUTCGUBRTCVFFZVBVGVDVICVFVASVLVCVHUOC
      VFUTGUCRTUOUTVFDAMKUDUEDBMKUDUEDCMKUDUEUFUG $.
      $([20-Jan-97]$)
  $}

  ${
    subaddeq.1 $e |- A e. CC $.
    subaddeq.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    subaddeq   $p |- ( A + ( B - A ) ) = B $=
      ( cmin wceq caddc co subcl subadd cleqid mpbi ) BAEZMFAMGHBFBAMDCBADCIJMK
      L $.
      $([26-Nov-94]$)
  $}

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negid   $p |- ( A + ( - A ) ) = 0 _10 $=
      ( cneg caddc co cn0 cmin df-neg opreq2i 0cn subaddeq eqtr ) AACZDEAFAGZDE
      FMNADAHIAFBJKL $.
      $([26-Nov-94]$)
  $}

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of
       Apostol, p. 18. $)
    subneg   $p |- ( A - B ) = ( A + ( - B ) ) $=
      ( cmin cneg caddc co wceq negcl addcl subadd cn0 0cn negid addcoml
      opreq2i addid1 eqtr addass1l mpbir ) ABEABFZGHZIBUCGHAIABUCCDAUBCBDJZKZLU
      CBAUEDCAUBBACUDDCAUBBGHZGHAMGHAUFMAGBUBMDUDNBDOPQACRSTPUA $.
      $([26-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    $( Relationship between subtraction and negative.  Theorem I.3 of
       Apostol, p. 18. $)
    subnegt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) = ( A + ( - B ) ) ) $=
      ( vx cmin cneg caddc co wceq cv cn0 wcel cc wi wa cab subeq1 opreq1
      cleq12d subeq2 negeq opreq2d 0cn cldedg2 subneg cldedg32 ) ABDZABEZFGZH
      CIZJKZALKZMUIAKUKNMCOZBDZULUGFGZHULUJBLKZMUIBKUONMCOZDZULUPEZFGZHCABLJAUL
      HUFUMUHUNAULBPAULUGFQRBUPHZUMUQUNUSBUPULSUTUGURULFBUPTUARULUPCALJUBUCCBLJ
      UBUCUDUE $.
      $([21-Jan-97]$)
  $}

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       Apostol, p. 18. $)
    negneg   $p |- ( - ( - A ) ) = A $=
      ( cneg caddc co cn0 negcl negid opreq2i opreq1i addid2 eqtr addass2l
      addid1 3eqtr3 ) AACZPCZDEZDEAFDEQARFADPABGZHIAPQQBSPSGZTAPDEZQDEFQDEQUAFQ
      DABHJQTKLMABNO $.
      $([8-Feb-95]$)

    $( Subtraction of a number from itself. $)
    subid   $p |- ( A - A ) = 0 _10 $=
      ( cmin cneg caddc co cn0 subneg negid eqtr ) AACAADEFGAABBHABIJ $.
      $([26-Nov-94]$)
  $}

  $( Minus 0 equals 0. $)
  neg0 $p |- ( - 0 _10 ) = 0 _10 $=
      ( cn0 cneg cmin df-neg 0cn subid eqtr ) ABAACAADAEFG $.
      $([17-Jan-97]$)

  ${
    $d x y A $.  $( ` x y ` are dummy. $)
    negrcl.1 $e |- A e. RR $.
    $( Closure of negation of reals. $)
    negrcl   $p |- ( - A ) e. RR $=
      ( vx vy cv cr wcel caddc co cn0 wceq wa wex cneg cmin cc wb axresscn ssi
      wi wel cab cleq2 opreq2 cleq1d bi2bid 0cn recn cldedg2 subadd cldedg3
      syl df-neg cleq1i syl5bb cleq3 biimprcd sylbird imp 19.23aiv wrex
      df-rex axrnegex ax-mp mpbi ) CEZFGZAVFHIZJKZLZCMZANZFGZVJVMCVGVIVMVGVIVLV
      FKZVMVGJAOZVFKZVIVNVGVFPGZVPVIQZFPVFRSVRVODEJGVQTDCUAVQLTDUBZKZAVSHIZJKZQ
      DVFPJVFVSKZVPVTVIWBVFVSVOUCWCVHWAJVFVSAHUDUEUFJAVSUGABUHDVFPJUGUIUJUKULVL
      VOVFAUMUNUOVNVMVGVLVFFUPUQURUSUTVICFVAZVKVICFVBAFGWDCAVCBVDVEVD $.
      $([17-Jan-97]$)
  $}

  ${
    $d x A $.  $( ` x ` is dummy. $)
    $( Closure of negation of reals. $)
    negrclt $p |- ( A e. RR -> ( - A ) e. RR ) $=
      ( vx cneg cr wcel cv cn0 wi wa cab wceq negeq cleq3d ax0re cldedg2
      negrcl cldedg3 ) ACZDEBFZGEADEZHSAETIHBJZCZDEBADGAUAKRUBDAUALMUABADGNOPQ
      $.
      $([20-Jan-97]$)
  $}

  $( Closure of subtraction of reals. $)
  subrclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
      ( cr wcel wa cmin cneg caddc co cc wceq subnegt axresscn ssi syl2an
      cleq3d axaddrcl negrclt sylan5 mpbird ) ACDZBCDZEZABFZCDABGZHIZCDZUCUDUFC
      AJDBJDUDUFKUAUBABLCJAMNCJBMNOPUAUECDUGUBAUEQBRST $.
      $([20-Jan-97]$)

  ${
    subrcl.1 $e |- A e. RR $.
    subrcl.2 $e |- B e. RR $.
    $( Closure of subtraction of reals. $)
    subrcl   $p |- ( A - B ) e. RR $=
      ( cr wcel cmin subrclt mp2an ) AEFBEFABGEFABHCDI $.
      $([17-Jan-97]$)
  $}

