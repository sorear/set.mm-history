$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Subtraction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w $.
    $( Define subtraction.  Theorem ~ subadd to relates it to addition, and
       theorems ~ subcl and ~subrcl prove its closure laws. s$)
    df-sub $a |- - = { <. <. x , y >. , z >. |
                   ( ( x e. CC /\ y e. CC ) /\
                   z = U. { w e. CC | ( y + w ) = x } ) } $.
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    subval.1 $e |- A e. CC $.
    subval.2 $e |- B e. CC $.
    $( Value of subtraction, which is the (unique) element ` x ` such that
       ` B + x = A `. $)
    subval $p |- ( A - B ) = U. { x e. CC | ( B + x ) = A } $=
      ( vy vz vw cc wcel cmin co cv caddc wceq crab cuni axcnex zfausrab zfun2
      cleq2 rbiabdv unieqd opreq1 cleq1d df-sub oprabval2 mp2an ) BIJCIJBCKLCAM
      ZNLZBOZAIPZQZODEFGHBCIIGMZUINLZFMZOZAIPZQUMKUOBOZAIPZQULUKAIRSTUPBOZURUTV
      AUQUSAIUPBUOUAUBUCUNCOZUTULVBUSUKAIVBUOUJBUNCUINUDUEUBUCFGHAUFUGUH $.
      $([8-May-99]$)
  $}

  $( Define the negative of a number (unary minus).  We use a different
     symbols from subtraction because it is a prefix operation with one
     argument, whereas subtraction is an infix operation with two arguments. $)
  df-neg $a |- -u A = ( 0_10 - A ) $.

  $( Equality theorem for negatives. $)
  negeq $p |- ( A = B -> -u A = -u B ) $=
    ( wceq cn0 cmin co cneg opreq2 df-neg 3eqtr4g ) ABCDAEFDBEFAGBGABDEHAIBIJ
    $.
    $([10-Feb-95]$)

  ${
    negeqi.1 $e |- A = B $.
    $( Equality inference for negatives. $)
    negeqi   $p |- -u A = -u B $=
      ( wceq cneg negeq ax-mp ) ABDAEBEDCABFG $.
      $([14-Feb-95]$)
  $}

  ${
    negeqd.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for negatives. $)
    negeqd   $p |- ( ph -> -u A = -u B ) $=
      ( wceq cneg negeq syl ) ABCEBFCFEDBCGH $.
      $([14-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.  $( ` x ` is dummy. $)
    subcl.1 $e |- A e. CC $.
    subcl.2 $e |- B e. CC $.
    $( Closure law for subtraction. $)
    subcl   $p |- ( A - B ) e. CC $=
      ( vx cmin co cv caddc wceq cc crab cuni subval wreu wcel negeux reucl
      ax-mp eqeltr ) ABFGBEHIGAJZEKLMZKEABCDNUAEKOUBKPEBADCQUAEKRST $.
      $([26-Nov-94]$)
  $}

  $( Closure law for subtraction. $)
  subclt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) e. CC ) $=
    ( cc wcel cmin co cn0 cded wceq opreq1 eleq1d opreq2 0cn elimel subcl
    dedth2h ) ACDZBCDZABEFZCDQAGHZBEFZCDTRBGHZEFZCDABGGATISUACATBEJKBUBIUAUCCBU
    BTELKTUBAGCMNBGCMNOP $.
    $([10-May-99]$)

  ${
    negcl.1 $e |- A e. CC $.
    $( Closure law for negation. $)
    negcl   $p |- -u A e. CC $=
      ( cneg cn0 cmin co cc df-neg 0cn subcl eqeltr ) ACDAEFGAHDAIBJK $.
      $([26-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( ` x ` is dummy. $)
    subadd.1 $e |- A e. CC $.
    subadd.2 $e |- B e. CC $.
    subadd.3 $e |- C e. CC $.
    $( Relationship between subtraction and addition. $)
    subadd   $p |- ( ( A - B ) = C <-> ( B + C ) = A ) $=
      ( vx cc wcel cmin co wceq caddc wb cv cleq2 opreq2 cleq1d bi2bid crab
      cuni wreu negeux reuuni mpan2 cleqcom subval cleq2i bitr syl6rbbr
      vtoclga ax-mp ) CHIABJKZCLZBCMKZALZNZFUMGOZLZBURMKZALZNUQGCHURCLZUSUNVAUP
      URCUMPVBUTUOAURCBMQRSURHIZVAURVAGHTUAZLZUSVCVAGHUBVAVENGBAEDUCVAGHUDUEUSU
      RUMLVEUMURUFUMVDURGABDEUGUHUIUJUKUL $.
      $([26-Nov-94]$)

    $( Relationship between subtraction and subtraction. $)
    subsub $p |- ( ( A - B ) = C <-> ( A - C ) = B ) $=
      ( caddc co wceq cmin addcom cleq1i subadd 3bitr4 ) BCGHZAICBGHZAIABJHCIAC
      JHBIOPABCEFKLABCDEFMACBDFEMN $.
      $([7-Oct-99]$)
  $}

  $( Relationship between subtraction and addition. $)
  subaddt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A - B ) = C <-> ( B + C ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb cn0 cded opreq1 cleq1d cleq2 bi2bid opreq2
    0cn elimel subadd dedth3h ) ADEZBDEZCDEZABFGZCHZBCIGZAHZJUBAKLZBFGZCHZUGUIH
    ZJABCKKKUIUCBKLZFGZCHZUMCIGZUIHZJUNUDCKLZHZUMURIGZUIHZJAUIHZUFUKUHULVBUEUJC
    AUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIMNPCURHZUOUSUQVACURU
    NOVDUPUTUICURUMIQNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $([20-Jan-97]$)

  ${
    subaddeq.1 $e |- A e. CC $.
    subaddeq.2 $e |- B e. CC $.
    $( Subtraction and addition of equals. $)
    subaddeq   $p |- ( A + ( B - A ) ) = B $=
      ( cmin co wceq caddc cleqid subcl subadd mpbi ) BAEFZMGAMHFBGMIBAMDCBADCJ
      KL $.
      $([26-Nov-94]$)
  $}

  ${
    negid.1 $e |- A e. CC $.
    $( Addition of a number and its negative. $)
    negid   $p |- ( A + -u A ) = 0_10 $=
      ( cneg caddc co cn0 cmin df-neg opreq2i 0cn subaddeq eqtr ) AACZDEAFAGEZD
      EFMNADAHIAFBJKL $.
      $([26-Nov-94]$)
  $}

  ${
    subneg.1 $e |- A e. CC $.
    subneg.2 $e |- B e. CC $.
    $( Relationship between subtraction and negative.  Theorem I.3 of
       Apostol, p. 18. $)
    subneg   $p |- ( A - B ) = ( A + -u B ) $=
      ( cmin co cneg caddc wceq negcl addcl cn0 0cn negid addcoml opreq2i
      addid1 eqtr addass1l subadd mpbir ) ABEFABGZHFZIBUCHFAIUCBAAUBCBDJZKZDCAU
      BBACUDDCAUBBHFZHFALHFAUFLAHBUBLDUDMBDNOPACQRSOABUCCDUETUA $.
      $([26-Nov-94]$)

    ${
      addsubass.3 $e |- C e. CC $.
      $( Associative-type law for subtraction and addition. $)
      addsubass $p |- ( ( A + B ) - C ) = ( A + ( B - C ) ) $=
        ( caddc co cneg cmin negcl addass addcl subneg opreq2i 3eqtr4 ) ABGHZCI
        ZGHABRGHZGHQCJHABCJHZGHABRDECFKLQCABDEMFNTSAGBCEFNOP $.
        $([16-Sep-99]$)

      $( Law for subtraction and addition. $)
      addsub $p |- ( ( A + B ) - C ) = ( ( A - C ) + B )  $=
        ( caddc co cmin addcom opreq1i addsubass subcl 3eqtr ) ABGHZCIHBAGHZCIH
        BACIHZGHQBGHOPCIABDEJKBACEDFLBQEACDFMJN $.
        $([19-Aug-01]$)
    $}
  $}

  $( Relationship between subtraction and negative.  Theorem I.3 of
     Apostol, p. 18. $)
  subnegt $p |- ( ( A e. CC /\ B e. CC ) -> ( A - B ) = ( A + -u B ) ) $=
    ( cc wcel cmin co cneg caddc wceq cn0 cded opreq1 cleq12d opreq2 negeq
    opreq2d 0cn elimel subneg dedth2h ) ACDZBCDZABEFZABGZHFZIUAAJKZBEFZUFUDHFZI
    UFUBBJKZEFZUFUIGZHFZIABJJAUFIUCUGUEUHAUFBELAUFUDHLMBUIIZUGUJUHULBUIUFENUMUD
    UKUFHBUIOPMUFUIAJCQRBJCQRST $.
    $([21-Jan-97]$)

  $( Law for subtraction and addition. $)
  addsubt $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                  ( ( A + B ) - C ) = ( ( A - C ) + B ) ) $=
    ( cc wcel caddc co cmin wceq cn0 cded opreq1 opreq1d cleq12d opreq2 0cn
    elimel addsub dedth3h ) ADEZBDEZCDEZABFGZCHGZACHGZBFGZITAJKZBFGZCHGZUGCHGZB
    FGZIABCJJJUGUABJKZFGZCHGZUJULFGZIUMUBCJKZHGZUGUPHGZULFGZIAUGIZUDUIUFUKUTUCU
    HCHAUGBFLMUTUEUJBFAUGCHLMNBULIZUIUNUKUOVAUHUMCHBULUGFOMBULUJFONCUPIZUNUQUOU
    SCUPUMHOVBUJURULFCUPUGHOMNUGULUPAJDPQBJDPQCJDPQRS $.
    $([19-Aug-01]$)

  ${
    negneg.1 $e |- A e. CC $.
    $( A number is equal to the negative of its negative.  Theorem I.4 of
       Apostol, p. 18. $)
    negneg   $p |- -u -u A = A $=
      ( cneg caddc co cn0 negcl negid opreq2i opreq1i addid2 eqtr addass2l
      addid1 3eqtr3 ) AACZPCZDEZDEAFDEQARFADPABGZHIAPQQBSPSGZTAPDEZQDEFQDEQUAFQ
      DABHJQTKLMABNO $.
      $([8-Feb-95]$)

    $( Subtraction of a number from itself. $)
    subid   $p |- ( A - A ) = 0_10 $=
      ( cmin co cneg caddc cn0 subneg negid eqtr ) AACDAAEFDGAABBHABIJ $.
      $([26-Nov-94]$)

    $( Identity law for subtraction. $)
    subid1 $p |- ( A - 0_10 ) = A $=
      ( cn0 cmin co wceq caddc addid2 0cn subadd mpbir ) ACDEAFCAGEAFABHACABIBJ
      K $.
      $([29-May-99]$)

  $}

  $( Subtraction of a number from itself. $)
  subidt $p |- ( A e. CC -> ( A - A ) = 0_10 ) $=
    ( cc wcel cmin co cn0 wceq cded opreq12 anabss cleq1d 0cn elimel subid
    dedth ) ABCZAADEZFGPAFHZRDEZFGAFARGZQSFTQSGARARDIJKRAFBLMNO $.
    $([8-Oct-99]$)


  ${
    subeq0.1 $e |- A e. CC $.
    subeq0.2 $e |- B e. CC $.
    $( If the difference between two numbers is zero, they are equal. $)
    subeq0 $p |- ( ( A - B ) = 0_10 <-> A = B ) $=
      ( cmin co cn0 wceq cneg caddc subneg cleq1i opreq1 sylbi negcl add23
      addass negid opreq2i addid1 eqtr 3eqtr addid2 3eqtr3g subid cleq2i biimp
      syl impbi ) ABEFZGHZABHZUKABIZJFZBJFZGBJFZABUKUNGHUOUPHUJUNGABCDKLUNGBJMN
      UOABJFUMJFABUMJFZJFZAAUMBCBDOZDPABUMCDUSQURAGJFAUQGAJBDRSACTUAUBBDUCUDULU
      JBBEFZHZUKABBEMVAUKUTGUJBDUEUFUGUHUI $.
      $([8-May-99]$)

    $( Negative is one-to-one. $)
    neg11 $p |- ( -u A = -u B <-> A = B ) $=
      ( cneg wceq cn0 cmin co caddc df-neg cleq12i 0cn subcl subadd subneg
      opreq2i eqtr2 cleq1i subeq0 bitr 3bitr ) AEZBEZFGAHIZGBHIZFAUFJIZGFZABFZU
      CUEUDUFAKBKZLGAUFMCGBMDNOUHABHIZGFUIUGUKGUKAUDJIUGABCDPUDUFAJUJQRSABCDTUA
      UB $.
      $([1-Aug-99]$)

    $( Negative contraposition law. $)
    negcon1 $p |- ( -u A = B <-> -u B = A ) $=
      ( cneg wceq negneg cleq1i negcl neg11 cleqcom 3bitr3 ) AEZEZBEZFAOFMBFOAF
      NAOACGHMBACIDJAOKL $.
      $([25-Aug-99]$)

    $( Negative contraposition law. $)
    negcon2 $p |- ( A = -u B <-> B = -u A ) $=
      ( cneg wceq negcon1 cleqcom 3bitr4 ) BEZAFAEZBFAJFBKFBADCGAJHBKHI $.
      $([25-Aug-99]$)
  $}

  $( If the difference between two numbers is zero, they are equal. $)
  subeq0t $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) = 0_10 <-> A = B ) ) $=
    ( cc wcel cmin co cn0 wceq wb cded opreq1 cleq1d cleq1 bi2bid opreq2 cleq2
    0cn elimel subeq0 dedth2h ) ACDZBCDZABEFZGHZABHZIUAAGJZBEFZGHZUFBHZIUFUBBGJ
    ZEFZGHZUFUJHZIABGGAUFHZUDUHUEUIUNUCUGGAUFBEKLAUFBMNBUJHZUHULUIUMUOUGUKGBUJU
    FEOLBUJUFPNUFUJAGCQRBGCQRST $.
    $([16-Nov-99]$)

  $( Minus 0 equals 0. $)
  neg0 $p |- -u 0_10 = 0_10 $=
    ( cn0 cneg cmin co df-neg 0cn subid eqtr ) ABAACDAAEAFGH $.
    $([17-Jan-97]$)

  ${
    $d x A $.  $( ` x ` is dummy. $)
    negrcl.1 $e |- A e. RR $.
    $( Closure law for negation of reals. $)
    negrcl   $p |- -u A e. RR $=
      ( vx cv cr wcel caddc co cn0 wceq wa wex cneg wrex axrnegex ax-mp df-rex
      mpbi cmin cc wb recnt cded cleq2 opreq2 cleq1d bi2bid 0cn recn elimel
      subadd dedth syl df-neg cleq1i syl5bb eleq1a sylbird imp 19.23aiv ) CDZEF
      ZAVAGHZIJZKZCLZAMZEFZVDCENZVFAEFVIBCAOPVDCEQRVEVHCVBVDVHVBVDVGVAJZVHVBIAS
      HZVAJZVDVJVBVATFZVLVDUAZVAUBVMVNVKVMVAIUCZJZAVOGHZIJZUAVAIVAVOJZVLVPVDVRV
      AVOVKUDVSVCVQIVAVOAGUEUFUGIAVOUHABUIVAITUHUJUKULUMVGVKVAAUNUOUPVAEVGUQURU
      SUTP $.
      $([17-Jan-97]$)
  $}

  $( Closure law for negation of reals. $)
  negrclt $p |- ( A e. RR -> -u A e. RR ) $=
    ( cr wcel cneg cn0 cded wceq negeq eleq1d ax0re elimel negrcl dedth ) ABCZA
    DZBCNAEFZDZBCAEAPGOQBAPHIPAEBJKLM $.
    $([20-Jan-97]$)

  $( Closure law for subtraction of reals. $)
  subrclt $p |- ( ( A e. RR /\ B e. RR ) -> ( A - B ) e. RR ) $=
    ( cr wcel wa cmin co cneg caddc axaddrcl negrclt sylan5 cc wceq subnegt
    recnt syl2an eleq1d mpbird ) ACDZBCDZEZABFGZCDABHZIGZCDZTUDCDUFUAAUDJBKLUBU
    CUECAMDBMDUCUENTUAABOAPBPQRS $.
    $([20-Jan-97]$)

  ${
    subrcl.1 $e |- A e. RR $.
    subrcl.2 $e |- B e. RR $.
    $( Closure law for subtraction of reals. $)
    subrcl   $p |- ( A - B ) e. RR $=
      ( cr wcel cmin co subrclt mp2an ) AEFBEFABGHEFCDABIJ $.
      $([17-Jan-97]$)
  $}

