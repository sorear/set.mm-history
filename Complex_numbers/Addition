$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Addition
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of Apostol, p. 18. $)
    addcan   $p |- ( ( A + B ) = ( A + C ) -> B = C ) $=
      ( vx cv caddc co cn0 wceq cc wrex wi wcel wa wb axaddass mp3an3 cleq12d
      mpan2 opreq2 syl5bir adantr axaddcom mpan cleq1d opreq1 addid2 syl6eq
      syl6bi imp sylibd exp r19.23aiv negex ax-mp ) AGHZIJZKLZGMNABIJZACIJZLZB
      CLZOZVAVFGMUSMPZVAVFVGVAQVDUSAIJZBIJZVHCIJZLZVEVGVDVKOVAVGVKUSVBIJZUSVCIJ
      ZLZVDVGAMPZVKVNRVGVOQVIVLVJVMVGVOBMPVIVLLUSABSETVGVOCMPVJVMLUSACSFTUADUBV
      BVCUSIUCUDUEVGVAVKVERZVGVAVHKLZVPVGUTVHKVOVGUTVHLAUSUFDUGUHVQVIBVJCVQVIKB
      IJBVHKBIUIBEUJUKVQVJKCIJCVHKCIUICFUJUKUAULUMUNUOUPGADUQUR $.
      $([21-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    $( Cancellation law for addition.  Theorem I.1 of Apostol, p. 18.  This
       proof illustrates how ~cldedg2 and ~cldedg33 can be used to convert
       the assumptions of ~addcan into antecedents.  This general
       method can be used to convert deductions into theorems as needed. $)
    addcant   $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                   ( ( A + B ) = ( A + C ) -> B = C ) ) $=
      ( vx caddc co wceq wi cv cn0 wcel cc wa cab opreq1 cleq12d birimd
      opreq2 cleq1d cleq1 bi2imd cleq2d cleq2 0cn cldedg2 addcan cldedg33 )
      ABEFZACEFZGZBCGZHDIZJKZALKZHULAKUNMHDNZBEFZUOCEFZGZUKHUOUMBLKZHULBKUSMHDN
      ZEFZUQGZUTCGZHVAUOUMCLKZHULCKVDMHDNZEFZGZUTVEGZHDABCJLAUOGZUJURUKVIUHUPUI
      UQAUOBEOAUOCEOPQBUTGZURVBUKVCVJUPVAUQBUTUOERSBUTCTUACVEGZVBVGVCVHVKUQVFVA
      CVEUOERUBCVEUTUCUAUOUTVEDALJUDUEDBLJUDUEDCLJUDUEUFUG $.
      $([22-Nov-94]$)
  $}

  ${
    $d x y A $.  $d x y B $. $( ` y ` is dummy. $)
    negeux.1 $e |- A e. CC $.
    negeux.2 $e |- B e. CC $.
    $( Existential uniqueness of negatives.  Theorem 1.2 of Apostol,
       p. 18. $)
    negeux   $p |- E! x e. CC ( A + x ) = B $=
      ( vy cv caddc co wceq cc wreu wrex wa weq wi wral opreq2 cleq1d reu4
      cn0 wcel axaddass mp3an1 mpan2 cleqcomd sylan9req opreq1 addid2 syl6eq
      exp32 comm imp a1i r19.21aiv exp r19.22 syl6 com23 axaddcl risset sylib
      mpd r19.23aiv negex ax-mp w3a addcant cleq2 biimparc syl5 rgen2 pm3.2i
      mpbir ) BAGZHIZCJZAKLVQAKMZVQBFGZHIZCJZNZAFOZPZFKQAKQZNVQWAAFKWCVPVTCVOVS
      BHRSTVRWEVTUAJZFKMVRWFVRFKVSKUBZVOVSCHIZJZAKMZWFVRPWGWFWJVRWGWFWIVQPZAKQZ
      WJVRPWGWFWLWGWFNZWKAKWMWKPVOKUBZWGWFWKWFWGWKWFWGWIVQWGWINWFVPVTCHIZCWIWGV
      PBWHHIZWOVOWHBHRWGWOWPWGCKUBZWOWPJZBKUBZWGWQWRBVSCUCDUDEUEUFUGWFWOUACHICV
      TUACHUHCEUIUJUGUKULUMUNUOUPWIVQAKUQURUSWGWHKUBZWJWGWQWTVSCUTEUEAWHKVAVBVC
      VDFBDVEVFWDAFKWSWNWGWDWSWNWGVGVPVTJZWCWBBVOVSVHWAXAVQVTCVPVIVJVKDUDVLVMVN
      $.
      $([22-Nov-94]$)
  $}

  ${
    add.1 $e |- A e. CC $.
    add.2 $e |- B e. CC $.
    add.3 $e |- C e. CC $.
    $( Commutative/associative law. $)
    add12 $p |- ( A + ( B + C ) ) = ( B + ( A + C ) ) $=
      ( caddc co addcl addcom opreq1i addass2r addass2l ) ABCBACGHZGHDEFBNEACDF
      IIBACABGHZCGHEDFOCABDEIFIOBAGHCGABDEJKLM $.
      $([21-Jan-97]$)

    $( Commutative/associative law. $)
    add23 $p |- ( ( A + B ) + C ) = ( ( A + C ) + B ) $=
      ( caddc co addcl addcom opreq2i addass1r addass1l ) ABCACGHZBGHDEFNBACDFI
      EIACBABCGHZGHDFEAODBCEFIIOCBGHAGBCEFJKLM $.
      $([21-Jan-97]$)
  $}




