$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Addition
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of Apostol, p. 18. $)
    addcanOBS   $p |- ( ( A + B ) = ( A + C ) -> B = C ) $=
      ( vx cv caddc co cn0 wceq cc wrex wi negex wcel wa wb axaddass mp3an3
      cleq12d mpan2 opreq2 syl5bir adantr axaddcom mpan cleq1d opreq1 addid2
      syl6eq syl6bi imp sylibd exp r19.23aiv ax-mp ) AGHZIJZKLZGMNABIJZACIJZLZB
      CLZOZGADPVAVFGMUSMQZVAVFVGVARVDUSAIJZBIJZVHCIJZLZVEVGVDVKOVAVGVKUSVBIJZUS
      VCIJZLZVDVGAMQZVKVNSDVGVORVIVLVJVMVGVOBMQVIVLLEUSABTUAVGVOCMQVJVMLFUSACTU
      AUBUCVBVCUSIUDUEUFVGVAVKVESZVGVAVHKLZVPVGUTVHKVOVGUTVHLDAUSUGUHUIVQVIBVJC
      VQVIKBIJBVHKBIUJBEUKULVQVJKCIJCVHKCIUJCFUKULUBUMUNUOUPUQUR $.
      $([21-Nov-94]$)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $( x is dummy. $)
    addcan.1 $e |- A e. CC $.
    addcan.2 $e |- B e. CC $.
    addcan.3 $e |- C e. CC $.
    $( Cancellation law for addition.  Theorem I.1 of Apostol, p. 18. $)
    addcan $p |- ( ( A + B ) = ( A + C ) <-> B = C ) $=
      ( vx caddc co wceq cv cn0 cc wrex wi negex wcel wa wb axaddass mp3an3
      cleq12d mpan2 opreq2 syl5bir adantr axaddcom mpan cleq1d opreq1 addid2
      syl6eq syl6bi imp sylibd exp r19.23aiv ax-mp impbi ) ABHIZACHIZJZBCJZAGKZ
      HIZLJZGMNVBVCOZGADPVFVGGMVDMQZVFVGVHVFRVBVDAHIZBHIZVICHIZJZVCVHVBVLOVFVHV
      LVDUTHIZVDVAHIZJZVBVHAMQZVLVOSDVHVPRVJVMVKVNVHVPBMQVJVMJEVDABTUAVHVPCMQVK
      VNJFVDACTUAUBUCUTVAVDHUDUEUFVHVFVLVCSZVHVFVILJZVQVHVEVILVPVHVEVIJDAVDUGUH
      UIVRVJBVKCVRVJLBHIBVILBHUJBEUKULVRVKLCHICVILCHUJCFUKULUBUMUNUOUPUQURBCAHU
      DUS $.
      $([27-Oct-99]$)
  $}

  $( Cancellation law for addition.  Theorem I.1 of Apostol, p. 18.  This
     proof illustrates how ~ dedth3h can be used to convert
     the assumptions of ~ addcan into antecedents.  This general
     method can be used to convert deductions into theorems as needed. $)
  addcant $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
                ( ( A + B ) = ( A + C ) <-> B = C ) ) $=
    ( cc wcel caddc co wceq wb cn0 cded opreq1 cleq12d birbid opreq2 cleq1d
    cleq1 bi2bid cleq2d cleq2 0cn elimel addcan dedth3h ) ADEZBDEZCDEZABFGZACFG
    ZHZBCHZIUEAJKZBFGZULCFGZHZUKIABCJJJULUFBJKZFGZUNHZUPCHZIUQULUGCJKZFGZHZUPUT
    HZIAULHZUJUOUKVDUHUMUIUNAULBFLAULCFLMNBUPHZUOURUKUSVEUMUQUNBUPULFOPBUPCQRCU
    THZURVBUSVCVFUNVAUQCUTULFOSCUTUPTRULUPUTAJDUAUBBJDUAUBCJDUAUBUCUD $.
    $([22-Nov-94]$)

  ${
    $d x y A $.  $d x y B $. $( ` y ` is dummy. $)
    negeux.1 $e |- A e. CC $.
    negeux.2 $e |- B e. CC $.
    $( Existential uniqueness of negatives.  Theorem 1.2 of Apostol,
       p. 18. $)
    negeux   $p |- E! x e. CC ( A + x ) = B $=
      ( vy cv caddc co wceq cc wreu wrex wa weq wi wral cn0 negex wcel axaddcl
      mpan2 risset sylib opreq2 axaddass mp3an1 cleqcomd sylan9eqr opreq1
      addid2 syl6eq exp32 comm imp a1d r19.21aiv exp r19.22 syl6 com23 mpd
      r19.23aiv ax-mp w3a addcant cleq2 biimparc syl5bi rgen2 pm3.2i cleq1d
      reu4 mpbir ) BAGZHIZCJZAKLVQAKMZVQBFGZHIZCJZNZAFOZPZFKQAKQZNVRWEVTRJZFKMV
      RFBDSWFVRFKVSKTZVOVSCHIZJZAKMZWFVRPWGWHKTZWJWGCKTZWKEVSCUAUBAWHKUCUDWGWFW
      JVRWGWFWIVQPZAKQZWJVRPWGWFWNWGWFNZWMAKWOWMVOKTZWGWFWMWFWGWMWFWGWIVQWGWINW
      FVPVTCHIZCWIWGVPBWHHIZWQVOWHBHUEWGWQWRWGWLWQWRJZEBKTZWGWLWSDBVSCUFUGUBUHU
      IWFWQRCHICVTRCHUJCEUKULUIUMUNUOUPUQURWIVQAKUSUTVAVBVCVDWDAFKWTWPWGWDDWTWP
      WGVEVPVTJZWCWBBVOVSVFWAXAVQVTCVPVGVHVIUGVJVKVQWAAFKWCVPVTCVOVSBHUEVLVMVN
      $.
      $([22-Nov-94]$)
  $}

  $( Commutative/associative law. $)
  add23t $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
               ( ( A + B ) + C ) = ( ( A + C ) + B ) ) $=
    ( cc wcel w3a caddc co wceq wa axaddcom opreq2d 3adant1 axaddass 3com23
    3eqtr4d ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCK
    LMABCNQSRUDUCIACBNOP $.
    $([13-Nov-99]$)

  $( Rearrangement of 4 terms. $)
  add4t $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
              ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) ) $=
    ( cc wcel wa caddc co wceq w3a add23t opreq1d 3expa adantarr axaddass
    3expb axaddcl sylan an4s 3eqtr3d ) AEFZBEFZGZCEFZDEFZGZGABHIZCHIZDHIZACHIZB
    HIZDHIZUHCDHIHIZUKBDHIHIZUDUEUJUMJZUFUBUCUEUPUBUCUEKUIULDHABCLMNOUHEFZUGUJU
    NJZUDUQUEUFURUHCDPQABRSUBUEUCUFUMUOJZUKEFZUCUFGUSUBUEGUTUCUFUSUKBDPQACRSTUA
    $.
    $([13-Nov-99]$)

  ${
    add.1 $e |- A e. CC $.
    add.2 $e |- B e. CC $.
    add.3 $e |- C e. CC $.
    $( Commutative/associative law. $)
    add12 $p |- ( A + ( B + C ) ) = ( B + ( A + C ) ) $=
      ( caddc co addcom opreq1i addass 3eqtr3 ) ABGHZCGHBAGHZCGHABCGHGHBACGHGHM
      NCGABDEIJABCDEFKBACEDFKL $.
      $([21-Jan-97]$)

    $( Commutative/associative law. $)
    add23 $p |- ( ( A + B ) + C ) = ( ( A + C ) + B ) $=
      ( cc wcel caddc co wceq add23t mp3an ) AGHBGHCGHABIJCIJACIJBIJKDEFABCLM
      $.
      $([21-Jan-97]$)
  $}

  ${
    add4.1 $e |- A e. CC $.
    add4.2 $e |- B e. CC $.
    add4.3 $e |- C e. CC $.
    add4.4 $e |- D e. CC $.
    $( Rearrangement of 4 terms. $)
    add4 $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( B + D ) ) $=
      ( cc wcel wa caddc co wceq pm3.2i add4t mp2an ) AIJZBIJZKCIJZDIJZKABLMCDL
      MLMACLMBDLMLMNRSEFOTUAGHOABCDPQ $.
      $([9-May-99]$)

    $( Rearrangement of 4 terms. $)
    add41r3 $p |- ( ( A + B ) + ( C + D ) ) = ( ( A + C ) + ( D + B ) ) $=
      ( caddc co add4 addcom opreq2i eqtr ) ABIJCDIJIJACIJZBDIJZIJODBIJZIJABCDE
      FGHKPQOIBDFHLMN $.
      $([22-Aug-99]$)
  $}


  $( Identity law for addition. $)
  addid2t $p |- ( A e. CC -> ( 0_10 + A ) = A ) $=
    ( cc wcel cn0 caddc co wceq 0cn axaddcom mpan ax0id eqtrd ) ABCZDAEFZADEFZA
    DBCMNOGHDAIJAKL $.
    $([10-Aug-99]$)

