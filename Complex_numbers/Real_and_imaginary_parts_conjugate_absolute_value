$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate; absolute value
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)
  $c abs $. $( Absolute value function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include complex part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  $( Extend class notation to include a function for the absolute value
     (modulus) of a complex number. $)
  cabs $a class abs $.

  ${
    $d x y z w $.
    $( Define a function whose value is the real part of a complex number. $)
    df-re $a |- Re = { <. x , y >. | ( x e. CC /\ y = U. { z e. RR |
                E. w e. RR x = ( z + ( w x. i ) ) } ) } $.

    $( Define a function whose value is the imaginary part of a complex
       number. $)
    df-im $a |- Im = { <. x , y >. | ( x e. CC /\ y = U. { w e. RR |
                E. z e. RR A = ( z + ( w x. i ) ) } ) } $.

    $( Define the complex conjugate function. $)
    df-cj $a |- * = { <. x , y >. | ( x e. CC /\ y = ( ( Re ` x )
                - ( ( Im ` x ) x. i ) ) ) } $.

    $( Define the function for the absolute value (modulus) of a complex
       number. $)
    df-abs $a |- abs = { <. x , y >. | ( x e. CC /\ y = ( sqr `
                 ( x x. ( * ` x ) ) ) ) } $.
  $}

  ${
    $d x y z w A $.

    $( The value of the real part of a complex number. $)
    reval $p |- ( A e. CC -> ( Re ` A ) = U. { x e. RR |
                E. y e. RR A = ( x + ( y x. i ) ) } ) $=
      ( vz vw cv cni cmulc co caddc wceq cr wrex crab cuni cc cre axcnex
      axresscn ssexi zfausrab zfun2 cleq1 birexdv rbiabdv unieqd df-re
      opabval2 ) DECDFZAFBFGHIJIZKZBLMZALNZOCUJKZBLMZALNZOPQUPUOALLPRSTUAUBUICK
      ZUMUPUQULUOALUQUKUNBLUICUJUCUDUEUFDEABUGUH $.
      $([9-May-99]$)

    $( The value of the imaginary part of a complex number. $)
    imval $p |- ( A e. CC -> ( Im ` A ) = U. { y e. RR |
                E. x e. RR A = ( x + ( y x. i ) ) } ) $=
      ( vz vw cv cni cmulc co caddc wceq cr wrex crab cuni cc cim axcnex
      axresscn ssexi zfausrab zfun2 cleq1 birexdv rbiabdv unieqd df-im
      opabval2 ) DECDFZAFBFGHIJIZKZALMZBLNZOCUJKZALMZBLNZOPQUPUOBLLPRSTUAUBUICK
      ZUMUPUQULUOBLUQUKUNALUICUJUCUDUEUFDEABUIUGUH $.
      $([9-May-99]$)

    $( The real part of a complex number is real. $)
    reclt $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( vx vy cc wcel cre cfv cr cv cni cmulc co caddc wceq wrex crab cuni
      reval eleq1d wreu creur reucl syl syl5bir pm2.43i ) ADEZAFGZHEZUFUHABICIJ
      KLMLNCHOZBHPQZHEZUFUFUGUJHBCARSUFUIBHTUKBCAUAUIBHUBUCUDUE $.
      $([9-May-99]$)

    $( The imaginary part of a complex number is real. $)
    imclt $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( vx vy cc wcel cim cfv cr cv cni cmulc co caddc wceq wrex crab cuni
      imval eleq1d wreu creui reucl syl syl5bir pm2.43i ) ADEZAFGZHEZUFUHABICIJ
      KLMLNBHOZCHPQZHEZUFUFUGUJHBCARSUFUICHTUKBCAUAUICHUBUCUDUE $.
      $([9-May-99]$)

    $( Construct a complex number from its parts. $)
    replim $p |- ( A e. CC -> A = ( ( Re ` A ) + ( ( Im ` A ) x. i ) ) ) $=
      ( vx vy cc wcel cv cr wa cim cfv cni cmulc co caddc wceq cre wex wi
      imclt a1d ancld reclt ancrd im2and imp crut syl cleq12 cleqid mpbii syl5
      exp imp32 pm3.26d opreq1d cleq2d pm3.27 pm3.26 3syl mpbid 19.23advv wrex
      wb opreq1 opreq2d birexdv cleq1 bi2bid bilimd crab cuni wreu reuuni
      creui sylan5 imval adantl bitr4d vtoclga mpcom mpbiri df-rex sylib creur
      reval jca an4 bi2ex eeanv bitr sylibr mpd ) ADEZBFZGEZCFZGEZHZAWNAIJZKLMZ
      NMZOZAAPJZWPKLMZNMZOZHZHZCQBQZAXCWTNMZOZWMXHXKBCWMXHXKWMXHHZXBXKXLXAXJAXL
      WNXCWTNXLWNXCOZWSWPOZWMWRXGXMXNHZWMWRXGXORWMWRHZXAXEOZXOXGXPWOWSGEZHZXCGE
      ZWQHZHZXQXORWMWRYBWMWOXSWQYAWMWOXRWMXRWOASZTUAWMWQXTWMXTWQAUBZTUCUDUEWNWS
      XCWPUFUGXGAAOXQAXAAXEUHAUIUJUKULUMUNUOUPXLXHXGXBWMXHUQWRXGUQXBXFURUSUTULV
      AWMWOXBHZBQZWQXFHZCQZHZXIWMYFYHWMXBBGVBZYFWMYJWSWSOZXRWMYJYKVCZWMAWNXDNMZ
      OZBGVBZWPWSOZVCZRWMYLRCWSGYPYQYLWMYPYOYJYPYKYPYNXBBGYPYMXAAYPXDWTWNNWPWSK
      LVDVEUPVFWPWSWSVGVHVIWQWMYQWQWMHYOWPYOCGVJVKZOZYPWQYOCGVLYOYSVCWMYOCGVMBC
      AVNVOWMYPYSVCWQWMWSYRWPBCAVPUPVQVRULVSYCVTWSUIWAXBBGWBWCWMXFCGVBZYHWMYTXC
      XCOZXTWMYTUVAVCZWMYNCGVBZXMVCZRWMUVBRBXCGXMUVDUVBWMXMUVCYTXMUVAXMYNXFCGXM
      YMXEAWNXCXDNVDUPVFWNXCXCVGVHVIWOWMUVDWOWMHUVCWNUVCBGVJVKZOZXMWOUVCBGVLUVC
      UVFVCWMUVCBGVMBCAWDVOWMXMUVFVCWOWMXCUVEWNBCAWEUPVQVRULVSYDVTXCUIWAXFCGWBW
      CWFXIYEYGHZCQBQYIXHUVGBCWOWQXBXFWGWHYEYGBCWIWJWKWL $.
      $([10-May-99]$)

    $( Value of complex conjugate.  Definition 10-3.2 of Gleason, p. 132. $)
    cjval $p |- ( A e. CC -> ( * ` A ) = ( ( Re ` A ) -
                ( ( Im ` A ) x. i ) ) ) $=
      ( vx vy cv cre cfv cim cni cmulc co cmin cc ccj oprex wceq fveq2 opreq1d
      opreq12d df-cj opabval2 ) BCABDZEFZUAGFZHIJZKJAEFZAGFZHIJZKJLMUEUGKNUAAOZ
      UBUEUDUGKUAAEPUHUCUFHIUAAGPQRBCST $.
      $([10-May-99]$)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjclt $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc wcel ccj cfv cre cim cni cmulc co cmin cjval eleq1d wa cr reclt
      recnt syl imclt axicn a1i jca axmulcl subclt mpbird ) ABCZADEZBCAFEZAGEZH
      IJZKJZBCZUFUGUKBALMUFUHBCZUJBCZNULUFUMUNUFUHOCUMAPUHQRUFUIBCZHBCZNUNUFUOU
      PUFUIOCUOASUIQRUPUFTUAUBUIHUCRUBUHUJUDRUE $.
      $([10-May-99]$)

    $( The absolute value (modulus) of a complex number.  Proposition
       10-3.7(a) of Gleason, p. 133. $)
    absval $p |- ( A e. CC -> ( abs ` A ) = ( sqr ` ( A x. ( * ` A ) ) ) ) $=
      ( vx vy cv ccj cfv cmulc co csqr cc cabs fvex wceq id fveq2 opreq12d
      fveq2d df-abs opabval2 ) BCABDZTEFZGHZIFAAEFZGHZIFJKUDILTAMZUBUDIUETAUAUC
      GUENTAEOPQBCRS $.
      $([27-Jul-99]$)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recl $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr reclt ax-mp ) ACDAEFGDBAHI $.
      $([11-May-99]$)

    $( The imaginary part of a complex number is real (closure law). $)
    imcl $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imclt ax-mp ) ACDAEFGDBAHI $.
      $([11-May-99]$)

    $( Closure law for complex conjugate. $)
    cjcl $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjclt ax-mp ) ACDAEFCDBAGH $.
      $([11-May-99]$)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       Gleason, p. 132. $)
    crre $p |- ( Re ` ( A + ( B x. i ) ) ) = A $=
      ( cni cmulc co caddc cre cfv wceq cim wa cc wcel recn axicn mulcl addcl
      replim ax-mp cleqcomi recl imcl cru pm3.26i ) ABEFGZHGZIJZAKZUHLJZBKZUIUK
      EFGHGZUHKUJULMUHUMUHNOUHUMKAUGACPBEBDPQRSZUHTUAUBUIUKABUHUNUCUHUNUDCDUEUA
      UF $.
      $([10-May-99]$)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of Gleason, p. 132. $)
    crim $p |- ( Im ` ( A + ( B x. i ) ) ) = B $=
      ( cni cmulc co caddc cre cfv wceq cim wa cc wcel recn axicn mulcl addcl
      replim ax-mp cleqcomi recl imcl cru pm3.27i ) ABEFGZHGZIJZAKZUHLJZBKZUIUK
      EFGHGZUHKUJULMUHUMUHNOUHUMKAUGACPBEBDPQRSZUHTUAUBUIUKABUHUNUCUHUNUDCDUEUA
      UF $.
      $([10-May-99]$)
  $}

  ${
    cjcj.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of Gleason, p. 133. $)
    cjcj $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre cim cni cmulc co cmin cneg cc wcel wceq cjcl cjval ax-mp
      caddc recl recn imcl axicn mulcl subneg mulneg1 opreq2i 3eqtr4 fveq2i
      negrcl crre eqtr opreq1i crim 3eqtr opreq12i negneg negcl replim ) ACDZCD
      ZUSEDZUSFDZGHIZJIZAEDZAFDZGHIZKZJIZAUSLMUTVDNABOUSPQVAVEVCVHJVAVEVFKZGHIZ
      RIZEDVEUSVLEVEVGJIZVEVHRIUSVLVEVGVEABSZTZVFGVFABUAZTZUBUCZUDALMZUSVMNBAPQ
      VKVHVERVFGVQUBUEZUFUGZUHVEVJVNVFVPUIZUJUKVCVLFDZGHIVKVHVBWCGHUSVLFWAUHULW
      CVJGHVEVJVNWBUMULVTUNUOVEVHKZRIVEVGRIZVIAWDVGVERVGVRUPUFVEVHVOVGVRUQUDVSA
      WENBAURQUGUN $.
      $([11-May-99]$)

    $( A complex number times its conjugate. $)
    cnmulcjval $p |- ( A x. ( * ` A ) ) = ( ( ( Re ` A ) x. ( Re ` A ) )
                     + ( ( Im ` A ) x. ( Im ` A ) ) ) $=
      ( ccj cfv cmulc co cre cim cni caddc cneg cmin cc wcel wceq replim ax-mp
      recl recn imcl axicn mulcl subneg cjval mulneg1 opreq2i 3eqtr4 opreq12i
      negrcl crmult cn0 mulrcl mul2neg eqtr3 eqtr addcom mulcom negid 3eqtr
      opreq1i mulzer2 addrcl addid1 ) AACDZEFAGDZAHDZIEFZJFZVEVFKZIEFZJFZEFVEVE
      EFZVFVIEFZLFZVEVIEFZVFVEEFZJFZIEFZJFZVLVFVFEFZJFZAVHVDVKEAMNZAVHOBAPQVEVG
      LFZVEVGKZJFVDVKVEVGVEABRZSZVFIVFABTZSZUAUBUCWBVDWCOBAUDQVJWDVEJVFIWHUAUEU
      FUGUHVEVFVEVIWEWGWEVFWGUIZUJVSWAUKJFWAVNWAVRUKJVNVLVMKZJFWAVLVMVLVEVEWEWE
      ULZSVMVFVIWGWIULSUCWJVTVLJVIVIEFWJVTVFVIWHVIWISZUEVFVFWHWHUMUNUFUOVRUKIEF
      UKVQUKIEVQVPVOJFVPVPKZJFUKVOVPVOVEVIWEWIULSVPVFVEWGWEULSZUPVOWMVPJVOVIVEE
      FWMVEVIWFWLUQVFVEWHWFUEUOUFVPWNURUSUTIUAVAUOUHWAWAVLVTWKVFVFWGWGULVBSVCUO
      US $.
      $([11-May-99]$)

    $( Value of absolute value function. $)
    absval2 $p |- ( abs ` A ) = ( sqr ` ( ( ( Re ` A ) x. ( Re ` A ) )
                     + ( ( Im ` A ) x. ( Im ` A ) ) ) ) $=
      ( cabs cfv ccj cmulc co csqr cre cim caddc cc wcel wceq absval ax-mp
      cnmulcjval fveq2i eqtr ) ACDZAAEDFGZHDZAIDZUCFGAJDZUDFGKGZHDALMTUBNBAOPUA
      UEHABQRS $.
      $([28-Jul-99]$)

    $( A complex number times its conjugate is real. $)
    cnmulcjcl $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmulc co cr wcel cre cim caddc cnmulcjval eleq1i recl mulrcl
      imcl addrcl mpbir ) AACDEFZGHAIDZTEFZAJDZUBEFZKFZGHSUDGABLMUAUCTTABNZUEOU
      BUBABPZUFOQR $.
      $([11-May-99]$)

    $( A complex number times its conjugate is non-negative. $)
    cnmulge0 $p |- 0 _10 <_ ( A x. ( * ` A ) ) $=
      ( cn0 cre cfv cmulc co cim caddc ccj cle wbr recl mulrcl imcl addge0
      sqge0 mp2an cnmulcjval breqtrr ) CADEZUAFGZAHEZUCFGZIGZAAJEFGKCUBKLCUDKLC
      UEKLUBUDUAUAABMZUFNUCUCABOZUGNPUAUFQUCUGQRABST $.
      $([28-May-99]$)

    $( A real number equals its real part. $)
    rere $p |- ( A e. RR <-> A = ( Re ` A ) ) $=
      ( cr wcel cre cfv wceq cim cni cmulc co caddc cn0 cmin subrclt recl
      mpan2 recn imcl axicn mulcl subadd cc replim ax-mp cleqcomi mpbir eleq1i
      sylib rimul mpan syl opreq1d mulzer2 syl6eq opreq2d addid1 3eqtr3g eleq1
      mpbiri impbi ) ACDZAAEFZGZVBVCAHFZIJKZLKZVCMLKAVCVBVFMVCLVBVFMIJKMVBVEMIJ
      VBVFCDZVEMGZVBAVCNKZCDZVHVBVCCDZVKAVCOABPZQVJVFCVJVFGVGAGAVCVFBVCVMRZVEIV
      EABSZRTUAUBAVGAUCDAVGGBAUDUEUFZUGUHUIVECDVHVIVEUJVOUKULUMITUNUOUPVPVCVNUQ
      URVDVBVLAVCCUSVMUTVA $.
      $([28-May-99]$)

    $( The imaginary part of a real number is 0. $)
    reim0 $p |- ( A e. RR <-> ( Im ` A ) = 0 _10 ) $=
      ( cr wcel cim cfv cn0 wceq cni cmulc co cre caddc rere biimp cc replim
      ax-mp recl recn addid1 cleqcomi 3eqtr3g imcl axicn mulcl 0cn addcan syl
      mulcom mulzer1 3eqtr4g wne wn df-ne ine0 mpbir mulcan opreq1 mulzer2
      syl6eq opreq2d eleq1d mpbiri impbi ) ACDZAEFZGHZVFIVGJKZIGJKZHVHVFVGIJKZG
      VIVJVFALFZVKMKZVLGMKZHVKGHVFAVLVMVNVFAVLHABNOAPDAVMHBAQRZVNVLVLVLABSZTZUA
      UBZUCVLVKGVQVGIVGABUDTZUEUFUGUHUIIVGUEVSUJIUEUKULIVGGUEVSUGIGUMIGHUNIGUOU
      PUQURUIVHVFVLCDVHAVLCVHVMVNAVLVHVKGVLMVHVKGIJKGVGGIJUSIUEUTVAVBVOVRULVCVP
      VDVE $.
      $([29-May-99]$)

    $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
       Gleason, p. 133. $)
    recj $p |- ( A e. RR <-> A = ( * ` A ) ) $=
      ( cr wcel ccj cfv wceq cre cim cni cmulc co caddc cmin cn0 reim0 opreq1
      axicn mulzer2 syl6eq opreq2d cleq12d recl recn addid1 subid1 eqtr4
      mpbiri sylbi cc replim ax-mp cjval 3eqtr4g cneg imcl mulcl subneg eqtr
      cleq12i negcl addcan mulcom negrcl mulneg1 wne wn df-ne ine0 mpbir
      mulcan syl eqneg sylib sylibr impbi ) ACDZAAEFZGZVQAHFZAIFZJKLZMLZVTWBNLZ
      AVRVQWAOGZWCWDGZABPZWEWFVTOMLZVTONLZGWEWCWHWDWIWEWBOVTMWEWBOJKLOWAOJKQJRS
      TZUAWEWBOVTNWJUAUBWHVTWIVTVTABUCUDZUEVTWKUFUGUHUIAUJDZAWCGBAUKULZWLVRWDGB
      AUMULZUNVSWEVQVSWAWAUOZGZWEVSJWAKLZJWOKLZGWPVSWBWBUOZWQWRVSWCVTWSMLZGWBWS
      GAWCVRWTWMVRWDWTWNVTWBWKWAJWAABUPZUDZRUQZURUSUTVTWBWSWKXCWBXCVAVBUIJWARXB
      VCWRWOJKLWSJWORWOWAXAVDUDZVCWAJXBRVEUSUNJWAWORXBXDJOVFJOGVGJOVHVIVJVKVLWA
      XBVMVNWGVOVP $.
      $([29-May-99]$)

    ${
      redistadd.2 $e |- B e. CC $.
      $( Distribution of real part over addition. $)
      redistadd $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv cim cni cmulc cc wcel wceq replim ax-mp opreq12i
        recl recn imcl axicn mulcl add4 distr2 opreq2i eqtr4 eqtr fveq2i
        addrcl crre ) ABEFZGHAGHZBGHZEFZAIHZBIHZEFZJKFZEFZGHUNUKUSGUKULUOJKFZEF
        ZUMUPJKFZEFZEFZUSAVABVCEALMAVANCAOPBLMBVCNDBOPQVDUNUTVBEFZEFUSULUTUMVBU
        LACRZSUOJUOACTZSZUAUBUMBDRZSUPJUPBDTZSZUAUBUCURVEUNEUOUPJVHVKUAUDUEUFUG
        UHUNUQULUMVFVIUIUOUPVGVJUIUJUG $.
        $([28-Jul-99]$)

      $( Distribution of imaginary part over addition. $)
      imdistadd $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre cni cmulc cc wcel wceq replim ax-mp opreq12i
        recl recn imcl axicn mulcl add4 distr2 opreq2i eqtr4 eqtr fveq2i
        addrcl crim ) ABEFZGHAIHZBIHZEFZAGHZBGHZEFZJKFZEFZGHUQUKUSGUKULUOJKFZEF
        ZUMUPJKFZEFZEFZUSAVABVCEALMAVANCAOPBLMBVCNDBOPQVDUNUTVBEFZEFUSULUTUMVBU
        LACRZSUOJUOACTZSZUAUBUMBDRZSUPJUPBDTZSZUAUBUCURVEUNEUOUPJVHVKUAUDUEUFUG
        UHUNUQULUMVFVIUIUOUPVGVJUIUJUG $.
        $([28-Jul-99]$)

      $( Real part of a product. $)
      remul $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmulc co cre cfv cim cmin caddc cni cc wcel wceq replim ax-mp
        opreq12i recl imcl crmult eqtr fveq2i mulrcl subrcl addrcl crre ) ABEFZ
        GHAGHZBGHZEFZAIHZBIHZEFZJFZUIUMEFZULUJEFZKFZLEFKFZGHUOUHUSGUHUIULLEFKFZ
        UJUMLEFKFZEFUSAUTBVAEAMNAUTOCAPQBMNBVAODBPQRUIULUJUMACSZACTZBDSZBDTZUAU
        BUCUOURUKUNUIUJVBVDUDULUMVCVEUDUEUPUQUIUMVBVEUDULUJVCVDUDUFUGUB $.
        $([28-Jul-99]$)

      $( Imaginary part of a product. $)
      immul $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmulc co cim cfv cre cmin caddc cni cc wcel wceq replim ax-mp
        opreq12i recl imcl crmult eqtr fveq2i mulrcl subrcl addrcl crim ) ABEFZ
        GHAIHZBIHZEFZAGHZBGHZEFZJFZUIUMEFZULUJEFZKFZLEFKFZGHURUHUSGUHUIULLEFKFZ
        UJUMLEFKFZEFUSAUTBVAEAMNAUTOCAPQBMNBVAODBPQRUIULUJUMACSZACTZBDSZBDTZUAU
        BUCUOURUKUNUIUJVBVDUDULUMVCVEUDUEUPUQUIUMVBVEUDULUJVCVDUDUFUGUB $.
        $([28-Jul-99]$)

      $( Distribution of complex conjugate over addition.  Proposition
         10-3.4(a) of Gleason, p. 133. $)
      cjdistadd $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv cim cni cmulc cmin ccj redistadd imdistadd opreq1i
        imcl recn axicn distr2 eqtr opreq12i recl mulcl sub4 eqtr4 cc wcel
        wceq addcl cjval ax-mp 3eqtr4 ) ABEFZGHZUNIHZJKFZLFZAGHZAIHZJKFZLFZBGHZ
        BIHZJKFZLFZEFZUNMHZAMHZBMHZEFURUSVCEFZVAVEEFZLFVGUOVKUQVLLABCDNUQUTVDEF
        ZJKFVLUPVMJKABCDOPUTVDJUTACQRZVDBDQRZSTUAUBUSVAVCVEUSACUCRUTJVNSUDVCBDU
        CRVDJVOSUDUEUFUNUGUHVHURUIABCDUJUNUKULVIVBVJVFEAUGUHVIVBUICAUKULBUGUHVJ
        VFUIDBUKULUBUM $.
        $([28-Jul-99]$)

      $( Distribution of complex conjugate over multiplication.  Proposition
         10-3.4(c) of Gleason, p. 133. $)
      cjdistmul $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmulc co cre cfv cim cni cmin cneg caddc ccj recl mulrcl imcl subrcl
        recn addrcl axicn mulcl subneg mul2neg cleqcomi opreq2i mulneg1
        negdist negrcl negneg opreq1i 3eqtr3 opreq12i eqtr4 eqtr3 eqtr remul
        immul crmult 3eqtr4 cc wcel wceq cjval ax-mp ) ABEFZGHZVFIHZJEFZKFZAGHZ
        AIHZLZJEFZMFZBGHZBIHZLZJEFZMFZEFZVFNHZANHZBNHZEFVKVPEFZVLVQEFZKFZVKVQEF
        ZVLVPEFZMFZJEFZKFZWEVMVREFZKFZVKVREFZVMVPEFZMFZJEFZMFZVJWAWLWGWKLZMFWSW
        GWKWGWEWFVKVPACOZBDOZPVLVQACQZBDQZPRSWJJWJWHWIVKVQXAXDPZVLVPXCXBPZTSZUA
        UBUCWGWNWTWRMWFWMWEKWMWFVLVQVLXCSZVQXDSZUDUEUFWJLZJEFWTWRWJJXGUAUGXJWQJ
        EXJWHLZWILZMFWQWHWIWHXESWIXFSUHWOXKWPXLMVKLZLZVREFXMVQEFWOXKXMVQXMVKXAU
        ISXIUDXNVKVREVKVKXASZUJUKVKVQXOXIUGULVLVPXHVPXBSZUGUMUNUKUOUMUPVGWGVIWK
        KABCDUQVHWJJEABCDURUKUMVKVMVPVRXAVLXCUIXBVQXDUIUSUTVFVAVBWBVJVCABCDUBVF
        VDVEWCVOWDVTEVKVLJEFZKFZVKXQLZMFWCVOVKXQXOVLJXHUAUBUCAVAVBWCXRVCCAVDVEV
        NXSVKMVLJXHUAUGUFUTVPVQJEFZKFZVPXTLZMFWDVTVPXTXPVQJXIUAUBUCBVAVBWDYAVCD
        BVDVEVSYBVPMVQJXIUAUGUFUTUMUT $.
        $([28-Jul-99]$)
    $}

    $( Real part of negative. $)
    reneg $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv cim cni cmulc co caddc recl recn imcl axicn mulcl negdist
      cc wcel wceq replim ax-mp negeqi mulneg1 opreq2i 3eqtr4 fveq2i negrcl
      crre eqtr ) ACZDEADEZCZAFEZCZGHIZJIZDEULUJUPDUKUMGHIZJIZCULUQCZJIUJUPUKUQ
      UKABKZLUMGUMABMZLZNOPAURAQRAURSBATUAUBUOUSULJUMGVBNUCUDUEUFULUNUKUTUGUMVA
      UGUHUI $.
      $([2-Aug-99]$)

    $( The negative of a real is real. $)
    negre $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg cr wcel cre cfv wceq negcl rere reneg cleq2i recl recn neg11
      3bitr eleq1 mpbiri sylbi negrclt impbi ) ACZDEZADEZUCAAFGZHZUDUCUBUBFGZHU
      BUECZHUFUBABIJUGUHUBABKLAUEBUEABMZNOPUFUDUEDEAUEDQUIRSATUA $.
      $([11-Aug-99]$)

    $( Imaginary part of negative. $)
    imneg $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre cni cmulc co caddc recl recn imcl axicn mulcl negdist
      cc wcel wceq replim ax-mp negeqi mulneg1 opreq2i 3eqtr4 fveq2i negrcl
      crim eqtr ) ACZDEAFEZCZADEZCZGHIZJIZDEUNUJUPDUKUMGHIZJIZCULUQCZJIUJUPUKUQ
      UKABKZLUMGUMABMZLZNOPAURAQRAURSBATUAUBUOUSULJUMGVBNUCUDUEUFULUNUKUTUGUMVA
      UGUHUI $.
      $([2-Aug-99]$)

    $( Complex conjugate of negative. $)
    cjneg $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv cim cni cmulc co cmin ccj caddc recl negrcl recn imcl
      axicn mulcl negcl subneg reneg imneg opreq1i mulneg1 eqtr opreq12i
      negdist 3eqtr4 negeqi eqtr4 cc wcel wceq cjval ax-mp ) ACZDEZUPFEZGHIZJIZ
      ADEZAFEZGHIZJIZCZUPKEZAKEZCUTVAVCCZLIZCZVEVACZVHJIVKVHCLIUTVJVKVHVKVAABMZ
      NOVCVBGVBABPOZQRZSZTUQVKUSVHJABUAUSVBCZGHIVHURVPGHABUBUCVBGVMQUDUEUFVAVHV
      AVLOZVOUGUHVDVIVAVCVQVNTUIUJUPUKULVFUTUMABSUPUNUOVGVDAUKULVGVDUMBAUNUOUIU
      H $.
      $([2-Aug-99]$)
  $}

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of Gleason, p. 133. $)
  cjcjt $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cn0 cded fveq2 fveq2d id cleq12d 0cn elimel cjcj
    dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNOP $.
    $([29-Jul-99]$)

  $( Distribution of complex conjugate over addition.  Proposition 10-3.4(a) of
     Gleason, p. 133. $)
  cjdistaddt $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cn0 cded opreq1 fveq2d fveq2 opreq1d
    cleq12d opreq2 opreq2d 0cn elimel cjdistadd dedth2h ) ACDZBCDZABEFZGHZAGHZB
    GHZEFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUT
    UEUKGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJU
    OAJCSTBJCSTUAUB $.
    $([31-Jul-99]$)

  $( Distribution of complex conjugate over multiplication.  Proposition
     10-3.4(c) of Gleason, p. 133. $)
  cjdistmult $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmulc co ccj cfv wceq cn0 cded opreq1 fveq2d fveq2 opreq1d
    cleq12d opreq2 opreq2d 0cn elimel cjdistmul dedth2h ) ACDZBCDZABEFZGHZAGHZB
    GHZEFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUT
    UEUKGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJU
    OAJCSTBJCSTUAUB $.
    $([29-Jul-99]$)




  $( The real part of zero. $)
  re0 $p |- ( Re ` 0 _10 ) = 0 _10 $=
    ( cn0 cre cfv cr wcel wceq 0cn rere ax0re mpbi cleqcomi ) AABCZADEALFAGHIJK
    $.
    $([27-Jul-99]$)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0 _10 ) = 0 _10 $=
    ( cn0 cr wcel cim cfv wceq 0cn reim0 ax0re mpbi ) ABCADEAFAGHIJ $.
    $([27-Jul-99]$)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0 _10 ) = 0 _10 $=
    ( cn0 ccj cfv cr wcel wceq 0cn recj ax0re mpbi cleqcomi ) AABCZADEALFAGHIJK
    $.
    $([27-Jul-99]$)

  $( The absolute value of zero. $)
  abs0 $p |- ( abs ` 0 _10 ) = 0 _10 $=
    ( cn0 cabs cfv ccj cmulc co csqr cc wcel wceq 0cn absval ax-mp cjcl
    mulzer2 fveq2i sqr0 eqtr ) ABCZAADCZEFZGCZAAHISUBJKALMUBAGCAUAAGTAKNOPQRR
    $.
    $([27-Jul-99]$)

  ${
    abs00.1 $e |- A e. CC $.
    $( The absolute value of a number is zero iff the number is zero.
       Proposition 10-3.7(c) of Gleason, p. 133. $)
    abs00 $p |- ( ( abs ` A ) = 0 _10 <-> A = 0 _10 ) $=
      ( cabs cfv cn0 wceq cre cmulc co cim caddc csqr absval2 cleq1i cr wcel
      cle wbr wb sqr00t recl mulrcl imcl addrcl addge0 sqge0 mp2an wa add20
      recn sq0 bi2an cni opreq1 opreq2d sylan9eq 0cn axicn mulcl addid2
      mulzer2 eqtr syl6eq cc replim ax-mp syl5eq fveq2 cleq1d re0 mpbiri im0
      jca impbi 3bitr ) ACDZEFAGDZVQHIZAJDZVSHIZKIZLDZEFZWAEFZAEFZVPWBEABMNWAOP
      EWAQRZWCWDSWATVRVTVQVQABUAZWGUBZVSVSABUCZWIUBZUDEVRQRZEVTQRZWFVRVTWHWJUEV
      QWGUFZVSWIUFZUGUGWDVREFZVTEFZUHZVQEFZVSEFZUHZWEWKWLWDWQSVRVTWHWJUIWMWNUGW
      OWRWPWSVQVQWGUJUKVSVSWIUJUKULWTWEWTVQVSUMHIZKIZEAWTXBEEUMHIZKIZEWRWSXBEXA
      KIXDVQEXAKUNWSXAXCEKVSEUMHUNUOUPXDXCEXCEUMUQURUSUTUMURVAVBVCAVDPAXBFBAVEV
      FVGWEWRWSWEWREGDZEFWEVQXEEAEGVHVIVJVKWEWSEJDZEFWEVSXFEAEJVHVIVLVKVMVNVOVO
      $.
      $([28-Jul-99]$)

    $( Real closure of absolute value. $)
    abscl $p |- ( abs ` A ) e. RR $=
      ( cabs cfv cr wcel ccj cmulc co csqr cc wceq absval ax-mp eleq1i cn0 cle
      wbr cnmulge0 cnmulcjcl sqrcl mpbir ) ACDZEFAAGDHIZJDZEFZUCUEEAKFUCUELBAMN
      OPUDQRUFABSUDABTUANUB $.
      $([2-Aug-99]$)

    $( Absolute value is non-negative. $)
    absge0 $p |- 0 _10 <_ ( abs ` A ) $=
      ( cn0 ccj cfv cmulc co csqr cabs cle wbr cnmulge0 cnmulcjcl sqrge0 ax-mp
      cc wcel wceq absval breqtrr ) CAADEFGZHEZAIEZJCUAJKCUBJKABLUAABMNOAPQUCUB
      RBASOT $.
      $([2-Aug-99]$)

    $( Absolute value of negative. $)
    absneg $p |- ( abs ` -u A ) = ( abs ` A ) $=
      ( cneg ccj cfv cmulc co csqr cabs cjneg opreq2i cjcl mul2neg eqtr fveq2i
      cc wcel wceq negcl absval ax-mp 3eqtr4 ) ACZUCDEZFGZHEZAADEZFGZHEZUCIEZAI
      EZUEUHHUEUCUGCZFGUHUDULUCFABJKAUGBABLMNOUCPQUJUFRABSUCTUAAPQUKUIRBATUAUB
      $.
      $([2-Aug-99]$)
  $}

  ${
    absid.1 $e |- A e. RR $.

    $( A non-negative number is its own absolute value. $)
    absid $p |- ( 0 _10 <_ A -> ( abs ` A ) = A ) $=
      ( cn0 cle wbr cmulc co csqr cfv cabs sqrsqid ccj cc wcel wceq recn
      absval ax-mp cr recj mpbi opreq2i fveq2i eqtr4 syl5eq ) CADEAAFGZHIZAAJIZ
      ABKUHAALIZFGZHIZUGAMNUHUKOABPZAQRUFUJHAUIAFASNAUIOAULTBUAUBUCUDUE $.
      $([2-Aug-99]$)

    $( A negative number is the negative of its own absolute value. $)
    absnid $p |- ( A <_ 0 _10 -> ( abs ` A ) = -u A ) $=
      ( cn0 cle wbr cneg cabs cfv wceq ax0re leneg neg0 breq1i bitr negrcl
      absid recn absneg syl5eqr sylbi ) ACDEZCAFZDEZAGHZUBIUACFZUBDEUCACBJKUECU
      BDLMNUCUBGHUBUDUBABOPAABQRST $.
      $([2-Aug-99]$)

    $( A real number is less than or equal to its absolute value. $)
    leabs $p |- A <_ ( abs ` A ) $=
      ( cn0 cle wbr wo cabs cfv ax0re letri wceq absid recn abscl eqle
      cleqcoms syl letr absge0 mpan2 jaoi ax-mp ) CADEZACDEZFAAGHZDEZCAIBJUCUFU
      DUCUEAKUFABLUFAUEAUEBAABMZNZOPQUDCUEDEUFACUEBIUHRAUGSTUAUB $.
      $([2-Aug-99]$)
  $}


