$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Real and imaginary parts; conjugate; absolute value
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Re $. $( Real part of a complex number. $)
  $c Im $. $( Imaginary part of a complex number. $)
  $c * $. $( Complex conjugate function. $)
  $c abs $. $( Absolute value function. $)

  $( Extend class notation to include real part of a complex number. $)
  cre $a class Re $.

  $( Extend class notation to include complex part of a complex number. $)
  cim $a class Im $.

  $( Extend class notation to include complex conjugate function. $)
  ccj $a class * $.

  $( Extend class notation to include a function for the absolute value
     (modulus) of a complex number. $)
  cabs $a class abs $.

  ${
    $d x y z w $.
    $( Define a function whose value is the real part of a complex number.
       See ~ revalt for its value, ~ recl for its closure, and ~ replimt
       for its use in decomposing a complex number. $)
    df-re $a |- Re = { <. x , y >. | ( x e. CC /\ y = U. { z e. RR |
                E. w e. RR x = ( z + ( w x. i ) ) } ) } $.

    $( Define a function whose value is the imaginary part of a complex
       number. See ~ imvalt for its value, ~ imcl for its closure, and ~ replimt
       for its use in decomposing a complex number. $)
    df-im $a |- Im = { <. x , y >. | ( x e. CC /\ y = U. { w e. RR |
                E. z e. RR A = ( z + ( w x. i ) ) } ) } $.

    $( Define the complex conjugate function.  See ~ cjcl for its closure and
       ~ cjvalt for its value. $)
    df-cj $a |- * = { <. x , y >. | ( x e. CC /\ y = ( ( Re ` x )
                - ( ( Im ` x ) x. i ) ) ) } $.

    $( Define the function for the absolute value (modulus) of a complex
       number.  See ~ abscl for its closure and ~ absvalt or ~ absval2
       for its value. $)
    df-abs $a |- abs = { <. x , y >. | ( x e. CC /\ y = ( sqr `
                 ( x x. ( * ` x ) ) ) ) } $.
  $}

  ${
    $d x y z w A $.

    $( The value of the real part of a complex number. $)
    revalt $p |- ( A e. CC -> ( Re ` A ) = U. { x e. RR |
                E. y e. RR A = ( x + ( y x. i ) ) } ) $=
      ( vz vw cv cni cmulc co caddc wceq cr wrex crab cuni cc cre cleq1
      birexdv rbiabdv unieqd df-re axcnex axresscn ssexi zfausrab zfun2
      opabval2 ) DECDFZAFBFGHIJIZKZBLMZALNZOCUJKZBLMZALNZOPQUICKZUMUPUQULUOALUQ
      UKUNBLUICUJRSTUADEABUBUPUOALLPUCUDUEUFUGUH $.
      $([9-May-99]$)

    $( The value of the imaginary part of a complex number. $)
    imvalt $p |- ( A e. CC -> ( Im ` A ) = U. { y e. RR |
                E. x e. RR A = ( x + ( y x. i ) ) } ) $=
      ( vz vw cv cni cmulc co caddc wceq cr wrex crab cuni cc cim cleq1
      birexdv rbiabdv unieqd df-im axcnex axresscn ssexi zfausrab zfun2
      opabval2 ) DECDFZAFBFGHIJIZKZALMZBLNZOCUJKZALMZBLNZOPQUICKZUMUPUQULUOBLUQ
      UKUNALUICUJRSTUADEABUIUBUPUOBLLPUCUDUEUFUGUH $.
      $([9-May-99]$)

    $( The real part of a complex number is real. $)
    reclt $p |- ( A e. CC -> ( Re ` A ) e. RR ) $=
      ( vx vy cc wcel cre cfv cr cv cni cmulc co caddc wceq wrex crab cuni
      revalt eleq1d wreu creur reucl syl syl5bir pm2.43i ) ADEZAFGZHEZUFUHABICIJ
      KLMLNCHOZBHPQZHEZUFUFUGUJHBCARSUFUIBHTUKBCAUAUIBHUBUCUDUE $.
      $([9-May-99]$)

    $( The imaginary part of a complex number is real. $)
    imclt $p |- ( A e. CC -> ( Im ` A ) e. RR ) $=
      ( vx vy cc wcel cim cfv cr cv cni cmulc co caddc wceq wrex crab cuni
      imvalt eleq1d wreu creui reucl syl syl5bir pm2.43i ) ADEZAFGZHEZUFUHABICIJ
      KLMLNBHOZCHPQZHEZUFUFUGUJHBCARSUFUICHTUKBCAUAUICHUBUCUDUE $.
      $([9-May-99]$)

    $( Construct a complex number from its parts. $)
    replimt $p |- ( A e. CC -> A = ( ( Re ` A ) + ( ( Im ` A ) x. i ) ) ) $=
      ( vx vy cc wcel cv cr wa cim cfv cni cmulc co caddc wceq cre wex wrex
      cleqid wb imclt wi opreq1 opreq2d cleq2d birexdv cleq1 bi2bid bilimd
      crab cuni wreu reuuni creui sylan5 imvalt adantl bitr4d exp vtoclga
      mpcom mpbiri df-rex sylib reclt creur revalt jca an4 bi2ex eeanv bitr
      sylibr pm3.26 ad2antrr a1d ancld ancrd im2and imp crut syl cleq12 mpbii
      syl5 imp32 pm3.26d opreq1d mpbid 19.23advv mpd ) ADEZBFZGEZCFZGEZHZAWMAIJ
      ZKLMZNMZOZAAPJZWOKLMZNMZOZHZHZCQBQZAXBWSNMZOZWLWNXAHZBQZWPXEHZCQZHZXHWLXL
      XNWLXABGRZXLWLXPWRWROZWRSWRGEZWLXPXQTZAUAZWLAWMXCNMZOZBGRZWOWROZTZUBWLXSU
      BCWRGYDYEXSWLYDYCXPYDXQYDYBXABGYDYAWTAYDXCWSWMNWOWRKLUCUDUEUFWOWRWRUGUHUI
      WPWLYEWPWLHYCWOYCCGUJUKZOZYDWPYCCGULYCYGTWLYCCGUMBCAUNUOWLYDYGTWPWLWRYFWO
      BCAUPUEUQURUSUTVAVBXABGVCVDWLXECGRZXNWLYHXBXBOZXBSXBGEZWLYHYITZAVEZWLYBCG
      RZWMXBOZTZUBWLYKUBBXBGYNYOYKWLYNYMYHYNYIYNYBXECGYNYAXDAWMXBXCNUCUEUFWMXBX
      BUGUHUIWNWLYOWNWLHYMWMYMBGUJUKZOZYNWNYMBGULYMYQTWLYMBGUMBCAVFUOWLYNYQTWNW
      LXBYPWMBCAVGUEUQURUSUTVAVBXECGVCVDVHXHXKXMHZCQBQXOXGYRBCWNWPXAXEVIVJXKXMB
      CVKVLVMWLXGXJBCWLXGXJWLXGHZXAXJXFXAWLWQXAXEVNVOYSWTXIAYSWMXBWSNYSYNWRWOOZ
      WLWQXFYNYTHZWLWQXFUVAUBWLWQHZWTXDOZUVAXFUVBWNXRHZYJWPHZHZUVCUVAUBWLWQUVFW
      LWNUVDWPUVEWLWNXRWLXRWNXTVPVQWLWPYJWLYJWPYLVPVRVSVTWMWRXBWOWAWBXFAAOUVCAS
      AWTAXDWCWDWEUSWFWGWHUEWIUSWJWK $.
      $([10-May-99]$)

    $( Value of complex conjugate.  Definition 10-3.2 of Gleason, p. 132. $)
    cjvalt $p |- ( A e. CC -> ( * ` A ) = ( ( Re ` A ) -
                ( ( Im ` A ) x. i ) ) ) $=
      ( vx vy cv cre cfv cim cni cmulc co cmin cc ccj wceq fveq2 opreq1d
      opreq12d df-cj oprex opabval2 ) BCABDZEFZUAGFZHIJZKJAEFZAGFZHIJZKJLMUAANZ
      UBUEUDUGKUAAEOUHUCUFHIUAAGOPQBCRUEUGKST $.
      $([10-May-99]$)

    $( The conjugate of a complex number is a complex number (closure law). $)
    cjclt $p |- ( A e. CC -> ( * ` A ) e. CC ) $=
      ( cc wcel ccj cfv cre cim cni cmulc co cmin subclt reclt recnd axmulcl
      imclt axicn a1i sylan13 cjvalt eleq1d mpbird ) ABCZADEZBCAFEZAGEZHIJZKJZB
      CZUEBCUGBCZUIUCUEUGLUCUEAMNUFBCHBCZUJUCUFHOUCUFAPNUKUCQRSSUCUDUHBATUAUB
      $.
      $([10-May-99]$)

    $( The absolute value (modulus) of a complex number.  Proposition
       10-3.7(a) of Gleason, p. 133. $)
    absvalt $p |- ( A e. CC -> ( abs ` A ) = ( sqr ` ( A x. ( * ` A ) ) ) ) $=
      ( vx vy cv ccj cfv cmulc co csqr cc cabs wceq id fveq2 opreq12d fveq2d
      df-abs fvex opabval2 ) BCABDZTEFZGHZIFAAEFZGHZIFJKTALZUBUDIUETAUAUCGUEMTA
      ENOPBCQUDIRS $.
      $([27-Jul-99]$)
  $}

  ${
    recl.1 $e |- A e. CC $.
    $( The real part of a complex number is real (closure law). $)
    recl $p |- ( Re ` A ) e. RR $=
      ( cc wcel cre cfv cr reclt ax-mp ) ACDAEFGDBAHI $.
      $([11-May-99]$)

    $( The imaginary part of a complex number is real (closure law). $)
    imcl $p |- ( Im ` A ) e. RR $=
      ( cc wcel cim cfv cr imclt ax-mp ) ACDAEFGDBAHI $.
      $([11-May-99]$)

    $( Closure law for complex conjugate. $)
    cjcl $p |- ( * ` A ) e. CC $=
      ( cc wcel ccj cfv cjclt ax-mp ) ACDAEFCDBAGH $.
      $([11-May-99]$)

    $( Construct a complex number from its parts. $)
    replim $p |- A = ( ( Re ` A ) + ( ( Im ` A ) x. i ) ) $=
      ( cc wcel cre cfv cim cni cmulc co caddc wceq replimt ax-mp ) ACDAAEFAGFH
      IJKJLBAMN $.
      $([1-Oct-99]$)
  $}

  ${
    crre.1 $e |- A e. RR $.
    crre.2 $e |- B e. RR $.
    $( The real part of a complex number representation.  Definition 10-3.1 of
       Gleason, p. 132. $)
    crre $p |- ( Re ` ( A + ( B x. i ) ) ) = A $=
      ( cni cmulc co caddc cre cfv wceq cim wa recn axicn mulcl addcl replim
      cleqcomi recl imcl cru ax-mp pm3.26i ) ABEFGZHGZIJZAKZUFLJZBKZUGUIEFGHGZU
      FKUHUJMUFUKUFAUEACNBEBDNOPQZRSUGUIABUFULTUFULUACDUBUCUD $.
      $([10-May-99]$)

    $( The imaginary part of a complex number representation.  Definition
       10-3.1 of Gleason, p. 132. $)
    crim $p |- ( Im ` ( A + ( B x. i ) ) ) = B $=
      ( cni cmulc co caddc cre cfv wceq cim wa recn axicn mulcl addcl replim
      cleqcomi recl imcl cru ax-mp pm3.27i ) ABEFGZHGZIJZAKZUFLJZBKZUGUIEFGHGZU
      FKUHUJMUFUKUFAUEACNBEBDNOPQZRSUGUIABUFULTUFULUACDUBUCUD $.
      $([10-May-99]$)
  $}

  ${
    cjcj.1 $e |- A e. CC $.
    $( The conjugate of the conjugate is the original complex number.
       Proposition 10-3.4(e) of Gleason, p. 133. $)
    cjcj $p |- ( * ` ( * ` A ) ) = A $=
      ( ccj cfv cre cim cni cmulc co cmin cneg cc wcel wceq cjcl cjvalt ax-mp
      caddc recl recn imcl axicn mulcl subneg mulneg1 opreq2i 3eqtr4 fveq2i
      negrcl crre eqtr opreq1i crim 3eqtr opreq12i negneg negcl replim ) ACDZCD
      ZUSEDZUSFDZGHIZJIZAEDZAFDZGHIZKZJIZAUSLMUTVDNABOUSPQVAVEVCVHJVAVEVFKZGHIZ
      RIZEDVEUSVLEVEVGJIZVEVHRIUSVLVEVGVEABSZTZVFGVFABUAZTZUBUCZUDALMUSVMNBAPQV
      KVHVERVFGVQUBUEZUFUGZUHVEVJVNVFVPUIZUJUKVCVLFDZGHIVKVHVBWBGHUSVLFVTUHULWB
      VJGHVEVJVNWAUMULVSUNUOVEVHKZRIVEVGRIVIAWCVGVERVGVRUPUFVEVHVOVGVRUQUDABURU
      GUN $.
      $([11-May-99]$)

    $( The imaginary part of a real number is 0. $)
    reim0 $p |- ( A e. RR <-> ( Im ` A ) = 0_10 ) $=
      ( cr wcel cim cfv cn0 wceq cni cmulc co cre cmin recl subrclt mpan2
      caddc replim cleqcomi recn imcl axicn mulcl subadd mpbir eleq1i sylib
      rimul mpan syl opreq1 mulzer2 syl6eq opreq2d addid1 3eqtr4g eleq1d
      mpbiri impbi ) ACDZAEFZGHZUTVAIJKZCDZVBUTAALFZMKZCDZVDUTVECDZVGABNZAVEOPV
      FVCCVFVCHVEVCQKZAHAVJABRZSAVEVCBVEVITZVAIVAABUAZTUBUCUDUEUFUGVACDVDVBVMVA
      UHUIUJVBUTVHVIVBAVECVBVJVEGQKZAVEVBVCGVEQVBVCGIJKGVAGIJUKIUBULUMUNVKVNVEV
      EVLUOSUPUQURUS $.
      $([29-May-99]$)

    $( A real number equals its real part.  Proposition 10-3.4(f) of Gleason,
       p. 133. $)
    rere $p |- ( A e. RR <-> ( Re ` A ) = A ) $=
      ( cr wcel cre cfv wceq cn0 caddc co cim cni cmulc reim0 opreq1 sylbi
      axicn mulzer2 syl6eq opreq2d replim syl5req recl recn addid1 syl5eqr
      eleq1 mpbii impbi ) ACDZAEFZAGZUJUKHIJZAUKUJUKAKFZLMJZIJUMAUJUOHUKIUJUOHL
      MJZHUJUNHGUOUPGABNUNHLMOPLQRSTABUAUBUKUKABUCZUDUEUFULUKCDUJUQUKACUGUHUI
      $.
      $([27-Oct-99]$)

    $( A number is real iff it equals its complex conjugate.  Proposition
       10-3.4(f) of Gleason, p. 133. $)
    cjre $p |- ( A e. RR <-> ( * ` A ) = A ) $=
      ( cr wcel ccj cfv wceq cre cim cni cmulc co caddc cmin cn0 reim0 recl
      recn addid1 subid1 eqtr4 opreq1 axicn mulzer2 syl6eq opreq2d cleq12d
      mpbiri sylbi replim cc cjvalt ax-mp 3eqtr4g cneg imcl mulcl subneg eqtr
      cleq12i negcl addcanOBS mulcom negrcl mulneg1 wne wn ine0 df-ne mpbir
      mulcan syl eqneg sylib sylibr impbi cleqcom bitr ) ACDZAAEFZGZVTAGVSWAVSA
      HFZAIFZJKLZMLZWBWDNLZAVTVSWCOGZWEWFGZABPZWGWHWBOMLZWBONLZGWJWBWKWBWBABQRZ
      SWBWLTUAWGWEWJWFWKWGWDOWBMWGWDOJKLOWCOJKUBJUCUDUEZUFWGWDOWBNWMUFUGUHUIABU
      JZAUKDVTWFGBAULUMZUNWAWGVSWAWCWCUOZGZWGWAJWCKLZJWPKLZGWQWAWDWDUOZWRWSWAWE
      WBWTMLZGWDWTGAWEVTXAWNVTWFXAWOWBWDWLWCJWCABUPZRZUCUQZURUSUTWBWDWTWLXDWDXD
      VAVBUIJWCUCXCVCWSWPJKLWTJWPUCWPWCXBVDRZVCWCJXCUCVEUSUNJWCWPUCXCXEJOVFJOGV
      GVHJOVIVJVKVLWCXCVMVNWIVOVPAVTVQVR $.
      $([11-Oct-99]$)

    $( Real part of a complex conjugate. $)
    recj $p |- ( Re ` ( * ` A ) ) = ( Re ` A ) $=
      ( ccj cfv cre cim cneg cni cmulc co caddc cmin recl recn imcl axicn
      mulcl subneg cc wcel wceq cjvalt ax-mp mulneg1 opreq2i 3eqtr4 fveq2i
      negrcl crre eqtr ) ACDZEDAEDZAFDZGZHIJZKJZEDULUKUPEULUMHIJZLJZULUQGZKJUKU
      PULUQULABMZNUMHUMABOZNZPQRASTUKURUABAUBUCUOUSULKUMHVBPUDUEUFUGULUNUTUMVAU
      HUIUJ $.
      $([2-Oct-99]$)

    $( Imaginary part of a complex conjugate. $)
    imcj $p |- ( Im ` ( * ` A ) ) = -u ( Im ` A ) $=
      ( ccj cfv cim cre cneg cni cmulc co caddc cmin recl recn imcl axicn
      mulcl subneg cc wcel wceq cjvalt ax-mp mulneg1 opreq2i 3eqtr4 fveq2i
      negrcl crim eqtr ) ACDZEDAFDZAEDZGZHIJZKJZEDUNUKUPEULUMHIJZLJZULUQGZKJUKU
      PULUQULABMZNUMHUMABOZNZPQRASTUKURUABAUBUCUOUSULKUMHVBPUDUEUFUGULUNUTUMVAU
      HUIUJ $.
      $([2-Oct-99]$)

    ${
      readd.2 $e |- B e. CC $.
      $( Real part distributes over addition. $)
      readd $p |- ( Re ` ( A + B ) ) = ( ( Re ` A ) + ( Re ` B ) ) $=
        ( caddc co cre cfv cim cni cmulc replim opreq12i recl recn imcl axicn
        mulcl add4 distr2 opreq2i eqtr4 eqtr fveq2i addrcl crre ) ABEFZGHAGHZBG
        HZEFZAIHZBIHZEFZJKFZEFZGHUJUGUOGUGUHUKJKFZEFZUIULJKFZEFZEFZUOAUQBUSEACL
        BDLMUTUJUPUREFZEFUOUHUPUIURUHACNZOUKJUKACPZOZQRUIBDNZOULJULBDPZOZQRSUNV
        AUJEUKULJVDVGQTUAUBUCUDUJUMUHUIVBVEUEUKULVCVFUEUFUC $.
        $([28-Jul-99]$)

      $( Imaginary part distributes over addition. $)
      imadd $p |- ( Im ` ( A + B ) ) = ( ( Im ` A ) + ( Im ` B ) ) $=
        ( caddc co cim cfv cre cni cmulc replim opreq12i recl recn imcl axicn
        mulcl add4 distr2 opreq2i eqtr4 eqtr fveq2i addrcl crim ) ABEFZGHAIHZBI
        HZEFZAGHZBGHZEFZJKFZEFZGHUMUGUOGUGUHUKJKFZEFZUIULJKFZEFZEFZUOAUQBUSEACL
        BDLMUTUJUPUREFZEFUOUHUPUIURUHACNZOUKJUKACPZOZQRUIBDNZOULJULBDPZOZQRSUNV
        AUJEUKULJVDVGQTUAUBUCUDUJUMUHUIVBVEUEUKULVCVFUEUFUC $.
        $([28-Jul-99]$)

      $( Real part of a product. $)
      remul $p |- ( Re ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Re ` B ) ) - ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( cmulc co cre cfv cim cmin caddc cni replim opreq12i recl imcl crmult
        eqtr fveq2i mulrcl subrcl addrcl crre ) ABEFZGHAGHZBGHZEFZAIHZBIHZEFZJF
        ZUEUIEFZUHUFEFZKFZLEFKFZGHUKUDUOGUDUEUHLEFKFZUFUILEFKFZEFUOAUPBUQEACMBD
        MNUEUHUFUIACOZACPZBDOZBDPZQRSUKUNUGUJUEUFURUTTUHUIUSVATUAULUMUEUIURVATU
        HUFUSUTTUBUCR $.
        $([28-Jul-99]$)

      $( Imaginary part of a product. $)
      immul $p |- ( Im ` ( A x. B ) ) =
            ( ( ( Re ` A ) x. ( Im ` B ) ) + ( ( Im ` A ) x. ( Re ` B ) ) ) $=
        ( cmulc co cim cfv cre cmin caddc cni replim opreq12i recl imcl crmult
        eqtr fveq2i mulrcl subrcl addrcl crim ) ABEFZGHAIHZBIHZEFZAGHZBGHZEFZJF
        ZUEUIEFZUHUFEFZKFZLEFKFZGHUNUDUOGUDUEUHLEFKFZUFUILEFKFZEFUOAUPBUQEACMBD
        MNUEUHUFUIACOZACPZBDOZBDPZQRSUKUNUGUJUEUFURUTTUHUIUSVATUAULUMUEUIURVATU
        HUFUSUTTUBUCR $.
        $([28-Jul-99]$)

      $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
         Gleason, p. 133. $)
      cjadd $p |- ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) $=
        ( caddc co cre cfv cim cni cmulc cmin ccj readd imadd opreq1i imcl
        recn axicn distr2 eqtr opreq12i recl mulcl sub4 cc wcel wceq addcl
        cjvalt ax-mp 3eqtr4 ) ABEFZGHZUMIHZJKFZLFZAGHZAIHZJKFZLFZBGHZBIHZJKFZLF
        ZEFZUMMHZAMHZBMHZEFUQURVBEFZUTVDEFZLFVFUNVJUPVKLABCDNUPUSVCEFZJKFVKUOVL
        JKABCDOPUSVCJUSACQRZVCBDQRZSTUAUBURVBUTVDURACUCRVBBDUCRUSJVMSUDVCJVNSUD
        UEUAUMUFUGVGUQUHABCDUIUMUJUKVHVAVIVEEAUFUGVHVAUHCAUJUKBUFUGVIVEUHDBUJUK
        UBUL $.
        $([28-Jul-99]$)

      $( Complex conjugate distributes over multiplication.  Proposition
         10-3.4(c) of Gleason, p. 133. $)
      cjmul $p |- ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) $=
        ( cmulc co cre cfv cim cni cmin cneg caddc ccj recl mulrcl imcl subrcl
        recn addrcl axicn mulcl subneg mul2neg cleqcomi opreq2i mulneg1
        negdist negrcl negneg opreq1i 3eqtr3 opreq12i eqtr4 eqtr3 eqtr remul
        immul crmult 3eqtr4 cc wcel wceq cjvalt ax-mp ) ABEFZGHZVFIHZJEFZKFZAGHZ
        AIHZLZJEFZMFZBGHZBIHZLZJEFZMFZEFZVFNHZANHZBNHZEFVKVPEFZVLVQEFZKFZVKVQEF
        ZVLVPEFZMFZJEFZKFZWEVMVREFZKFZVKVREFZVMVPEFZMFZJEFZMFZVJWAWLWGWKLZMFWSW
        GWKWGWEWFVKVPACOZBDOZPVLVQACQZBDQZPRSWJJWJWHWIVKVQXAXDPZVLVPXCXBPZTSZUA
        UBUCWGWNWTWRMWFWMWEKWMWFVLVQVLXCSZVQXDSZUDUEUFWJLZJEFWTWRWJJXGUAUGXJWQJ
        EXJWHLZWILZMFWQWHWIWHXESWIXFSUHWOXKWPXLMVKLZLZVREFXMVQEFWOXKXMVQXMVKXAU
        ISXIUDXNVKVREVKVKXASZUJUKVKVQXOXIUGULVLVPXHVPXBSZUGUMUNUKUOUMUPVGWGVIWK
        KABCDUQVHWJJEABCDURUKUMVKVMVPVRXAVLXCUIXBVQXDUIUSUTVFVAVBWBVJVCABCDUBVF
        VDVEWCVOWDVTEVKVLJEFZKFZVKXQLZMFWCVOVKXQXOVLJXHUAUBUCAVAVBWCXRVCCAVDVEV
        NXSVKMVLJXHUAUGUFUTVPVQJEFZKFZVPXTLZMFWDVTVPXTXPVQJXIUAUBUCBVAVBWDYAVCD
        BVDVEVSYBVPMVQJXIUAUGUFUTUMUT $.
        $([28-Jul-99]$)

      $( Standard inner product on complex numbers. $)
      ipcn $p |- ( Re ` ( A x. ( * ` B ) ) ) =
             ( ( ( Re ` A ) x. ( Re ` B ) ) + ( ( Im ` A ) x. ( Im ` B ) ) ) $=
        ( ccj cfv cmulc co cre cim cmin cneg caddc cjcl remul recl mulrcl recn
        imcl subneg recj opreq2i wceq imcj negcon2 mpbi mulneg2 eqtr2 opreq12i
        3eqtr ) ABEFZGHIFAIFZUKIFZGHZAJFZUKJFZGHZKHUNUQLZMHULBIFZGHZUOBJFZGHZMH
        AUKCBDNZOUNUQUNULUMACPUKVCPQRUQUOUPACSZUKVCSZQRTUNUTURVBMUMUSULGBDUAUBV
        BUOUPLZGHURVAVFUOGUPVALUCVAVFUCBDUDUPVAUPVERZVABDSRUEUFUBUOUPUOVDRVGUGU
        HUIUJ $.
        $([2-Oct-99]$)
    $}

    $( A complex number times its conjugate is real. $)
    cjmulrcl $p |- ( A x. ( * ` A ) ) e. RR $=
      ( ccj cfv cmulc co cr wcel wceq cjcj opreq2i cjcl cjmul mulcom 3eqtr4
      mulcl cjre mpbir ) AACDZEFZGHTCDZTISSCDZEFSAEFUATUBASEABJKASBABLZMASBUCNO
      TASBUCPQR $.
      $([11-May-99]$)

    $( A complex number times its conjugate. $)
    cjmulval $p |- ( A x. ( * ` A ) ) =
                   ( ( ( Re ` A ) ^ 2_10 ) + ( ( Im ` A ) ^ 2_10 ) ) $=
      ( ccj cfv cmulc co cre cl2 cexp cim caddc cr wcel wceq cjmulrcl recn
      rere mpbi ipcn recl sqval imcl opreq12i eqtr4 eqtr3 ) AACDEFZGDZUFAGDZHIF
      ZAJDZHIFZKFZUFLMUGUFNABOZUFUFUMPQRUGUHUHEFZUJUJEFZKFULAABBSUIUNUKUOKUHUHA
      BTPUAUJUJABUBPUAUCUDUE $.
      $([2-Oct-99]$)

    $( A complex number times its conjugate is non-negative. $)
    cjmulge0 $p |- 0_10 <_ ( A x. ( * ` A ) ) $=
      ( cn0 cre cfv cl2 cexp co cim caddc ccj cmulc cle wbr recl sqege0 imcl
      sqrecl addge0 mp2an cjmulval breqtrr ) CADEZFGHZAIEZFGHZJHZAAKELHMCUDMNCU
      FMNCUGMNUCABOZPUEABQZPUDUFUCUHRUEUIRSTABUAUB $.
      $([28-May-99]$)

    $( Real part of negative. $)
    reneg $p |- ( Re ` -u A ) = -u ( Re ` A ) $=
      ( cneg cre cfv cim cni cmulc co caddc recl recn imcl axicn mulcl negdist
      replim negeqi mulneg1 opreq2i 3eqtr4 fveq2i negrcl crre eqtr ) ACZDEADEZC
      ZAFEZCZGHIZJIZDEUHUFULDUGUIGHIZJIZCUHUMCZJIUFULUGUMUGABKZLUIGUIABMZLZNOPA
      UNABQRUKUOUHJUIGURNSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $([2-Aug-99]$)

    $( The negative of a real is real. $)
    negre $p |- ( -u A e. RR <-> A e. RR ) $=
      ( cneg cr wcel cre cfv wceq negcl rere reneg cleq1i recl recn neg11
      3bitr eleq1 mpbii sylbi negrclt impbi ) ACZDEZADEZUCAFGZAHZUDUCUBFGZUBHUE
      CZUBHUFUBABIJUGUHUBABKLUEAUEABMZNBOPUFUEDEUDUIUEADQRSATUA $.
      $([11-Aug-99]$)

    $( Imaginary part of negative. $)
    imneg $p |- ( Im ` -u A ) = -u ( Im ` A ) $=
      ( cneg cim cfv cre cni cmulc co caddc recl recn imcl axicn mulcl negdist
      replim negeqi mulneg1 opreq2i 3eqtr4 fveq2i negrcl crim eqtr ) ACZDEAFEZC
      ZADEZCZGHIZJIZDEUJUFULDUGUIGHIZJIZCUHUMCZJIUFULUGUMUGABKZLUIGUIABMZLZNOPA
      UNABQRUKUOUHJUIGURNSTUAUBUHUJUGUPUCUIUQUCUDUE $.
      $([2-Aug-99]$)

    $( Complex conjugate of negative. $)
    cjneg $p |- ( * ` -u A ) = -u ( * ` A ) $=
      ( cneg cre cfv cim cni cmulc co cmin ccj caddc recl negrcl recn imcl
      axicn mulcl negcl subneg reneg imneg opreq1i mulneg1 eqtr opreq12i
      negdist 3eqtr4 negeqi eqtr4 cc wcel wceq cjvalt ax-mp ) ACZDEZUPFEZGHIZJIZ
      ADEZAFEZGHIZJIZCZUPKEZAKEZCUTVAVCCZLIZCZVEVACZVHJIVKVHCLIUTVJVKVHVKVAABMZ
      NOVCVBGVBABPOZQRZSZTUQVKUSVHJABUAUSVBCZGHIVHURVPGHABUBUCVBGVMQUDUEUFVAVHV
      AVLOZVOUGUHVDVIVAVCVQVNTUIUJUPUKULVFUTUMABSUPUNUOVGVDAUKULVGVDUMBAUNUOUIU
      H $.
      $([2-Aug-99]$)

    $( A number plus its conjugate is twice its real part.  Compare Proposition
       10-3.4(h) of Gleason, p. 133. $)
    addcj $p |- ( A + ( * ` A ) ) = ( 2_10 x. ( Re ` A ) ) $=
      ( cre cfv cim cni cmulc co caddc cneg ccj cl2 cn0 recl recn imcl axicn
      mulcl negcl add4 negid opreq2i addcl addid1 3eqtr replim cmin cc wcel
      wceq cjvalt ax-mp subneg eqtr opreq12i 2times 3eqtr4 ) ACDZAEDZFGHZIHZURU
      TJZIHZIHZURURIHZAAKDZIHLURGHVDVEUTVBIHZIHVEMIHVEURUTURVBURABNOZUSFUSABPOQ
      RZVHUTVISTVGMVEIUTVIUAUBVEURURVHVHUCUDUEAVAVFVCIABUFVFURUTUGHZVCAUHUIVFVJ
      UJBAUKULURUTVHVIUMUNUOURVHUPUQ $.
      $([2-Oct-99]$)
  $}

  $( A real number equals its complex conjugate.  Proposition 10-3.4(f) of
     Gleason, p. 133. $)
  cjret $p |- ( A e. RR -> ( * ` A ) = A ) $=
    ( cr wcel ccj cfv wceq cc wi recnt wb cn0 cded eleq1 fveq2 id cleq12d
    bi2bid 0cn elimel cjre dedth biimpd syl pm2.43i ) ABCZADEZAFZUEAGCZUEUGHAIU
    HUEUGUHUEUGJUHAKLZBCZUIDEZUIFZJAKAUIFZUEUJUGULAUIBMUMUFUKAUIAUIDNUMOPQUIAKG
    RSTUAUBUCUD $.
    $([8-Oct-99]$)

  $( The conjugate of the conjugate is the original complex number.
     Proposition 10-3.4(e) of Gleason, p. 133. $)
  cjcjt $p |- ( A e. CC -> ( * ` ( * ` A ) ) = A ) $=
    ( cc wcel ccj cfv wceq cn0 cded fveq2 fveq2d id cleq12d 0cn elimel cjcj
    dedth ) ABCZADEZDEZAFQAGHZDEZDEZTFAGATFZSUBATUCRUADATDIJUCKLTAGBMNOP $.
    $([29-Jul-99]$)

  $( Complex conjugate distributes over addition.  Proposition 10-3.4(a) of
     Gleason, p. 133. $)
  cjaddt $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A + B ) ) = ( ( * ` A ) + ( * ` B ) ) ) $=
    ( cc wcel caddc co ccj cfv wceq cn0 cded opreq1 fveq2d fveq2 opreq1d
    cleq12d opreq2 opreq2d 0cn elimel cjadd dedth2h ) ACDZBCDZABEFZGHZAGHZB
    GHZEFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUT
    UEUKGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJU
    OAJCSTBJCSTUAUB $.
    $([31-Jul-99]$)

  $( Complex conjugate distributes over multiplication.  Proposition 10-3.4(c)
     of Gleason, p. 133. $)
  cjmult $p |- ( ( A e. CC /\ B e. CC ) ->
                   ( * ` ( A x. B ) ) = ( ( * ` A ) x. ( * ` B ) ) ) $=
    ( cc wcel cmulc co ccj cfv wceq cn0 cded opreq1 fveq2d fveq2 opreq1d
    cleq12d opreq2 opreq2d 0cn elimel cjmul dedth2h ) ACDZBCDZABEFZGHZAGHZB
    GHZEFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUT
    UEUKGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJU
    OAJCSTBJCSTUAUB $.
    $([29-Jul-99]$)

  $( The real part of zero. $)
  re0 $p |- ( Re ` 0_10 ) = 0_10 $=
    ( cn0 cr wcel cre cfv wceq ax0re 0cn rere mpbi ) ABCADEAFGAHIJ $.
    $([27-Jul-99]$)

  $( The imaginary part of zero. $)
  im0 $p |- ( Im ` 0_10 ) = 0_10 $=
    ( cn0 cr wcel cim cfv wceq ax0re 0cn reim0 mpbi ) ABCADEAFGAHIJ $.
    $([27-Jul-99]$)

  $( The conjugate of zero. $)
  cj0 $p |- ( * ` 0_10 ) = 0_10 $=
    ( cn0 cr wcel ccj cfv wceq ax0re 0cn cjre mpbi ) ABCADEAFGAHIJ $.
    $([27-Jul-99]$)

  ${
    absval2.1 $e |- A e. CC $.
    $( Value of absolute value function.  Definition 10.36 of Gleason,
       p. 133. $)
    absval2 $p |- ( abs ` A ) =
                 ( sqr ` ( ( ( Re ` A ) ^ 2_10 ) + ( ( Im ` A ) ^ 2_10 ) ) ) $=
      ( cabs cfv ccj cmulc co csqr cre cl2 cexp cim caddc cc wcel wceq absvalt
      ax-mp cjmulval fveq2i eqtr ) ACDZAAEDFGZHDZAIDJKGALDJKGMGZHDANOUBUDPBAQRU
      CUEHABSTUA $.
      $([2-Oct-99]$)

    $( Square of value of absolute value function. $)
    absvalsq $p |- ( ( abs ` A ) ^ 2_10 ) = ( A x. ( * ` A ) ) $=
      ( cabs cfv cl2 cexp co ccj cmulc csqr cc wcel wceq absvalt ax-mp opreq1i
      cn0 cle wbr cjmulge0 cjmulrcl sqsqr eqtr ) ACDZEFGAAHDIGZJDZEFGZUEUDUFEFA
      KLUDUFMBANOPQUERSUGUEMABTUEABUAUBOUC $.
      $([2-Oct-99]$)

    $( Square of value of absolute value function. $)
    absvalsq2 $p |- ( ( abs ` A ) ^ 2_10 ) =
                    ( ( ( Re ` A ) ^ 2_10 ) + ( ( Im ` A ) ^ 2_10 ) ) $=
      ( cabs cfv cl2 cexp co ccj cmulc cre cim caddc absvalsq cjmulval eqtr )
      ACDEFGAAHDIGAJDEFGAKDEFGLGABMABNO $.
      $([2-Oct-99]$)

    $( The absolute value of a number is zero iff the number is zero.
       Proposition 10-3.7(c) of Gleason, p. 133. $)
    abs00 $p |- ( ( abs ` A ) = 0_10 <-> A = 0_10 ) $=
      ( cabs cfv cn0 wceq cre cl2 cexp co cim caddc csqr absval2 cleq1i cr
      wcel cle wbr wb recl sqrecl imcl addrcl sqege0 addge0 mp2an sqr00t wa
      add20 recn sqe0 bi2an cni cmulc opreq1 opreq2d sylan9eq 0cn axicn mulcl
      addid2 mulzer2 eqtr syl6eq replim syl5eq re0 fveq2 cleq1d mpbiri im0 jca
      impbi 3bitr ) ACDZEFAGDZHIJZAKDZHIJZLJZMDZEFZWAEFZAEFZVPWBEABNOWAPQEWARSZ
      WCWDTVRVTVQABUAZUBZVSABUCZUBZUDEVRRSZEVTRSZWFVQWGUEZVSWIUEZVRVTWHWJUFUGWA
      UHUGWDVREFZVTEFZUIZVQEFZVSEFZUIZWEWKWLWDWQTWMWNVRVTWHWJUJUGWOWRWPWSVQVQWG
      UKULVSVSWIUKULUMWTWEWTVQVSUNUOJZLJZEAWTXBEEUNUOJZLJZEWRWSXBEXALJXDVQEXALU
      PWSXAXCELVSEUNUOUPUQURXDXCEXCEUNUSUTVAVBUNUTVCVDVEABVFVGWEWRWSWEWREGDZEFV
      HWEVQXEEAEGVIVJVKWEWSEKDZEFVLWEVSXFEAEKVIVJVKVMVNVOVO $.
      $([28-Jul-99]$)

    $( Real closure of absolute value. $)
    abscl $p |- ( abs ` A ) e. RR $=
      ( cabs cfv cr wcel ccj cmulc co csqr cn0 cle wbr cjmulge0 cjmulrcl
      sqrcl ax-mp cc wceq absvalt eleq1i mpbir ) ACDZEFAAGDHIZJDZEFZKUDLMUFABNUD
      ABOPQUCUEEARFUCUESBATQUAUB $.
      $([2-Aug-99]$)

    $( Absolute value is non-negative. $)
    absge0 $p |- 0_10 <_ ( abs ` A ) $=
      ( cn0 ccj cfv cmulc co csqr cabs cle wbr cjmulge0 cjmulrcl sqrge0 ax-mp
      cc wcel wceq absvalt breqtrr ) CAADEFGZHEZAIEZJCUAJKCUBJKABLUAABMNOAPQUCUB
      RBASOT $.
      $([2-Aug-99]$)

    $( The absolute value of a non-zero number is positive.  Apostol,
       p. 363. $)
    absgt0 $p |- ( -. A = 0_10 <-> 0_10 < ( abs ` A ) ) $=
      ( cn0 wceq wn cabs cfv clt wbr abs00 bicom cleqcom bitr bineg wo cle
      absge0 ax0re abscl leloe mpbi orcom ori ltne impbi ) ACDZECAFGZDZEZCUGHIZ
      UFUHUFUGCDZUHUKUFABJKUGCLMNUIUJUHUJUJUHOZUHUJOCUGPIULABQCUGRABSZTUAUJUHUB
      UAUCCUGRUMUDUEM $.
      $([1-Oct-99]$)

    $( Absolute value of negative. $)
    absneg $p |- ( abs ` -u A ) = ( abs ` A ) $=
      ( cneg ccj cfv cmulc co csqr cabs cjneg opreq2i cjcl mul2neg eqtr fveq2i
      cc wcel wceq negcl absvalt ax-mp 3eqtr4 ) ACZUCDEZFGZHEZAADEZFGZHEZUCIEZAI
      EZUEUHHUEUCUGCZFGUHUDULUCFABJKAUGBABLMNOUCPQUJUFRABSUCTUAAPQUKUIRBATUAUB
      $.
      $([2-Aug-99]$)

    $( The absolute value of a number and its conjugate are the same.
       Proposition 10-3.7(b) of Gleason, p. 133. $)
    abscj $p |- ( abs ` A ) = ( abs ` ( * ` A ) ) $=
      ( ccj cfv cmulc co csqr cabs cjcl mulcom cjcj opreq2i eqtr4 fveq2i cc
      wcel wceq absvalt ax-mp 3eqtr4 ) AACDZEFZGDZUAUACDZEFZGDZAHDZUAHDZUBUEGUB
      UAAEFUEAUABABIZJUDAUAEABKLMNAOPUGUCQBARSUAOPUHUFQUIUARST $.
      $([2-Oct-99]$)

    ${
      abssub.2 $e |- B e. CC $.
      $( Swapping order of subtraction doesn't change the absolute value.
         Apostol, p. 363. $)
      abssub $p |- ( abs ` ( A - B ) ) = ( abs ` ( B - A ) ) $=
        ( cmin co cneg cabs cfv negdist3 fveq2i subcl absneg eqtr3 ) BAEFZGZHIA
        BEFZHIOHIPQHBADCJKOBADCLMN $.
        $([1-Oct-99]$)

      $( Absolute value distributes over multiplication.  Proposition 10-3.7(f)
         of Gleason, p. 133. $)
      absmul $p |- ( abs ` ( A x. B ) ) = ( ( abs ` A ) x. ( abs ` B ) ) $=
        ( cmulc co ccj cfv csqr cabs cjmul opreq2i cjcl mul4 eqtr fveq2i
        cjmulrcl cjmulge0 sqrmuli cc wcel wceq mulcl absvalt ax-mp opreq12i
        3eqtr4 ) ABEFZUHGHZEFZIHZAAGHZEFZIHZBBGHZEFZIHZEFZUHJHZAJHZBJHZEFUKUMUP
        EFZIHURUJVBIUJUHULUOEFZEFVBUIVCUHEABCDKLABULUOCDACMBDMNOPUMUPACQBDQACRB
        DRSOUHTUAUSUKUBABCDUCUHUDUEUTUNVAUQEATUAUTUNUBCAUDUEBTUAVAUQUBDBUDUEUFU
        G $.
        $([1-Oct-99]$)

      $( Square of absolute value of sum.  Proposition 10-3.7(g) of Gleason,
         p. 133. $)
      sqabsadd $p |- ( ( abs ` ( A + B ) ) ^ 2_10 ) =
                   ( ( ( ( abs ` A ) ^ 2_10 ) + ( ( abs ` B ) ^ 2_10 ) ) +
                   ( 2_10 x. ( Re ` ( A x. ( * ` B ) ) ) ) )$=
        ( caddc co ccj cfv cmulc cabs cl2 cexp cre cjadd opreq2i cjcl muladd
        eqtr addcl absvalsq mulcom opreq12i mulcl addcj cjmul cjcj eqtr3
        3eqtr4 ) ABEFZUIGHZIFZAAGHZIFZBGHZBIFZEFZAUNIFZULBIFZEFZEFZUIJHKLFAJHKL
        FZBJHKLFZEFZKUQMHIFZEFUKUIULUNEFZIFUTUJVEUIIABCDNOABULUNCDACPBDPZQRUIAB
        CDSTVCUPVDUSEVAUMVBUOEACTVBBUNIFUOBDTBUNDVFUARUBUQUQGHZEFVDUSUQAUNCVFUC
        UDVGURUQEVGULUNGHZIFURAUNCVFUEVHBULIBDUFOROUGUBUH $.
        $([2-Oct-99]$)
    $}
  $}

  $( Real closure of absolute value. $)
  absclt $p |- ( A e. CC -> ( abs ` A ) e. RR ) $=
    ( cc wcel cabs cfv cr cn0 cded wceq fveq2 eleq1d 0cn elimel abscl dedth )
    ABCZADEZFCPAGHZDEZFCAGARIQSFARDJKRAGBLMNO $.
    $([3-Oct-99]$)

  $( Absolute value distributes over multiplication.  Proposition 10-3.7(f)
     of Gleason, p. 133. $)
  absmult $p |- ( ( A e. CC /\ B e. CC ) ->
                ( abs ` ( A x. B ) ) = ( ( abs ` A ) x. ( abs ` B ) ) ) $=
    ( cc wcel cmulc co cabs cfv wceq cn0 cded opreq1 fveq2d fveq2 opreq1d
    cleq12d opreq2 opreq2d 0cn elimel absmul dedth2h ) ACDZBCDZABEFZGHZAGHZBGHZ
    EFZIUCAJKZBEFZGHZUJGHZUHEFZIUJUDBJKZEFZGHZUMUOGHZEFZIABJJAUJIZUFULUIUNUTUEU
    KGAUJBELMUTUGUMUHEAUJGNOPBUOIZULUQUNUSVAUKUPGBUOUJEQMVAUHURUMEBUOGNRPUJUOAJ
    CSTBJCSTUAUB $.
    $([11-Oct-99]$)

  ${
    absid.1 $e |- A e. RR $.

    $( A non-negative number is its own absolute value. $)
    absid $p |- ( 0_10 <_ A -> ( abs ` A ) = A ) $=
      ( cn0 cle wbr cmulc co csqr cfv cabs sqrsqid ccj cc wcel wceq recn
      absvalt ax-mp cr cjre mpbi opreq2i fveq2i eqtr syl5eq ) CADEAAFGZHIZAAJIZ
      ABKUHAALIZFGZHIZUGAMNUHUKOABPZAQRUJUFHUIAAFASNUIAOBAULTUAUBUCUDUE $.
      $([2-Aug-99]$)

    $( A negative number is the negative of its own absolute value. $)
    absnid $p |- ( A <_ 0_10 -> ( abs ` A ) = -u A ) $=
      ( cn0 cle wbr cneg cabs cfv wceq ax0re leneg neg0 breq1i bitr negrcl
      absid recn absneg syl5eqr sylbi ) ACDEZCAFZDEZAGHZUBIUACFZUBDEUCACBJKUECU
      BDLMNUCUBGHUBUDUBABOPAABQRST $.
      $([2-Aug-99]$)

    $( A real number is less than or equal to its absolute value. $)
    leabs $p |- A <_ ( abs ` A ) $=
      ( cn0 cle wbr wo cabs cfv ax0re letri wceq absid recn abscl eqle
      cleqcoms syl absge0 letr mpan2 jaoi ax-mp ) CADEZACDEZFAAGHZDEZCAIBJUCUFU
      DUCUEAKUFABLUFAUEAUEBAABMZNZOPQUDCUEDEUFAUGRACUEBIUHSTUAUB $.
      $([2-Aug-99]$)

    $( The absolute value of a real number is either that number or
       its negative. $)
    absor $p |- ( ( abs ` A ) = A \/ ( abs ` A ) = -u A ) $=
      ( cn0 cle wbr wo cabs cfv wceq cneg ax0re letri absid absnid im2or ax-mp
      ) CADEZACDEZFAGHZAIZSAJIZFCAKBLQTRUAABMABNOP $.
      $([30-Sep-99]$)

    $( Absolute value of a real number. $)
    absre $p |- ( abs ` A ) = ( sqr ` ( A ^ 2_10 ) ) $=
      ( cabs cfv ccj cmulc co csqr cl2 cexp cc wcel wceq recn absvalt ax-mp
      sqval cr cjre mpbi opreq2i eqtr4 fveq2i ) ACDZAAEDZFGZHDZAIJGZHDAKLUDUGMA
      BNZAOPUHUFHUHAAFGUFAUIQUEAAFARLUEAMBAUISTUAUBUCUB $.
      $([3-Aug-99]$)

    ${
      abslt.1 $e |- B e. RR $.
      $( Absolute value and "less than" relation. $)
      abslt $p |- ( ( abs ` A ) < B <-> ( A < B /\ -u A < B ) ) $=
        ( cabs cfv clt wbr cneg wa cle leabs recn abscl lelttr mpan negrcl
        absneg breqtr jca wceq wo absor breq1 biimprcd jaao mpi impbi ) AEFZBGH
        ZABGHZAIZBGHZJZUJUKUMAUIKHUJUKACLAUIBCAACMZNZDOPULUIKHUJUMULULEFUIKULAC
        QZLAUORSULUIBUQUPDOPTUNUIAUAZUIULUAZUBUJACUCUKURUJUMUSURUJUKUIABGUDUEUS
        UJUMUIULBGUDUEUFUGUH $.
        $([3-Oct-99]$)

      $( Absolute value and "less than or equal" relation. $)
      absle $p |- ( ( abs ` A ) <_ B <-> ( A <_ B /\ -u A <_ B ) ) $=
        ( cabs cfv cle wbr cneg wa leabs recn abscl letr mpan negrcl absneg
        breqtr jca wceq wo absor breq1 biimprcd jaao mpi impbi ) AEFZBGHZABGHZA
        IZBGHZJZUIUJULAUHGHUIUJACKAUHBCAACLZMZDNOUKUHGHUIULUKUKEFUHGUKACPZKAUNQ
        RUKUHBUPUODNOSUMUHATZUHUKTZUAUIACUBUJUQUIULURUQUIUJUHABGUCUDURUIULUHUKB
        GUCUDUEUFUG $.
        $([3-Oct-99]$)
    $}
  $}

  $( Absolute value and "less than" relation. $)
  absltt $p |- ( ( A e. RR /\ B e. RR ) ->
               ( ( abs ` A ) < B <-> ( A < B /\ -u A < B ) ) ) $=
    ( cr wcel cabs cfv clt wbr cneg wa wb cn0 cded wceq fveq2 breq1d breq1
    negeq bi2and bi2bid breq2 ax0re elimel abslt dedth2h ) ACDZBCDZAEFZBGHZABGH
    ZAIZBGHZJZKUFALMZEFZBGHZUNBGHZUNIZBGHZJZKUOUGBLMZGHZUNVAGHZURVAGHZJZKABLLAU
    NNZUIUPUMUTVFUHUOBGAUNEOPVFUJUQULUSAUNBGQVFUKURBGAUNRPSTBVANZUPVBUTVEBVAUOG
    UAVGUQVCUSVDBVAUNGUABVAURGUASTUNVAALCUBUCBLCUBUCUDUE $.
    $([3-Oct-99]$)

  ${
    releabs.1 $e |- A e. CC $.
    $( The real part of a number is less than or equal to its absolute
       value.  Proposition 10-3.7(d) of Gleason, p. 133. $)
    releabs $p |- ( Re ` A ) <_ ( abs ` A ) $=
      ( cre cfv cabs cle wbr recl leabs cl2 cexp co cim caddc cn0 sqrecl recn
      addid1 imcl sqege0 ax0re leadd2 mpbi eqbrtrr csqr absre opreq1i wceq
      sqsqr ax-mp eqtr absvalsq2 3brtr4 wb absge0 abscl le2sqe mp2an mpbir
      letr ) ACDZVAEDZFGVBAEDZFGZVAVCFGVAABHZIVDVBJKLZVCJKLZFGZVAJKLZVIAMDZJKLZ
      NLZVFVGFVIONLZVIVLFVIVIVAVEPZQROVKFGVMVLFGVJABSZTOVKVIUAVJVOPVNUBUCUDVFVI
      UEDZJKLZVIVBVPJKVAVEUFUGOVIFGVQVIUHVAVETVIVNUIUJUKABULUMOVBFGOVCFGVDVHUNV
      AVAVEQZUOABUOVBVCVAVRUPZABUPZUQURUSVAVBVCVEVSVTUTUR $.
      $([2-Oct-99]$)

    ${
      abstri.2 $e |- B e. CC $.
      $( Triangle inequality for absolute value.  Proposition 10-3.7(h) of
         Gleason, p. 133. $)
      abstri $p |- ( abs ` ( A + B ) ) <_ ( ( abs ` A ) + ( abs ` B ) ) $=
        ( caddc co cabs cfv cle wbr cl2 cexp ccj cmulc cre cjcl mulcl releabs
        absmul abscj opreq2i eqtr4 breqtr cn0 clt wb 2pos recl abscl mulrcl
        2re lemul2 ax-mp mpbi sqrecl addrcl leadd2 sqabsadd recn binom sqcl
        add23 eqtr 3brtr4 addcl absge0 addge0 mp2an le2sqe mpbir ) ABEFZGHZAGHZ
        BGHZEFZIJZVLKLFZVOKLFZIJZVMKLFZVNKLFZEFZKABMHZNFZOHZNFZEFZWBKVMVNNFZNFZ
        EFZVQVRIWFWIIJZWGWJIJWEWHIJZWKWEWDGHZWHIWDAWCCBDPZQZRWMVMWCGHZNFWHAWCCW
        NSVNWPVMNBDTUAUBUCUDKUEJWLWKUFUGWEWHKWDWOUHZVMVNACUIZBDUIZUJZUKULUMUNWF
        WIWBKWEUKWQUJKWHUKWTUJZVTWAVMWRUOVNWSUOUPUQUNABCDURVRVTWIEFWAEFWJVMVNVM
        WRUSZVNWSUSZUTVTWIWAVMXBVAWIXAUSVNXCVAVBVCVDUDVLIJUDVOIJZVPVSUFVKABCDVE
        ZVFUDVMIJUDVNIJXDACVFBDVFVMVNWRWSVGVHVLVOVKXEUIVMVNWRWSUPVIVHVJ $.
        $([2-Oct-99]$)
      ${
        abs3dif.3 $e |- C e. CC $.
        $( Absolute value of differences around common element.  $)
        abs3dif $p |- ( abs ` ( A - B ) ) <_ ( ( abs ` ( A - C ) ) +
                       ( abs ` ( C - B ) ) ) $=
          ( cmin co cabs cfv caddc cle cneg negcl add4 cn0 addcl addid1 negid
          opreq2i subneg 3eqtr4r add41r3 3eqtr4 opreq12i eqtr4 fveq2i subcl
          abstri eqbrtr ) ABGHZIJACGHZCBGHZKHZIJULIJUMIJKHLUKUNIUKACMZKHZCBMZKH
          ZKHZUNAUQKHZCUOKHZKHZACKHUQUOKHKHUKUSAUQCUODBENZFCFNZOUTPKHUTVBUKUTAU
          QDVCQRVAPUTKCFSTABDEUAUBAUOCUQDVDFVCUCUDULUPUMURKACDFUACBFEUAUEUFUGUL
          UMACDFUHCBFEUHUIUJ $.
          $([2-Oct-99]$)

        ${
          abs3lem.4 $e |- D e. RR $.
          $( Lemma involving absolute value of differences. $)
          abs3lem $p |- ( ( ( abs ` ( A - C ) ) < ( D / 2_10 ) /\
                             ( abs ` ( C - B ) ) < ( D / 2_10 ) ) ->
                             ( abs ` ( A - B ) ) < D ) $=
            ( cmin co cabs cfv cl2 cdiv clt wbr wa caddc subcl abscl 2re 2pos
            gt0ne0i divrcl lt2add cle abs3dif addrcl lelttr mpan syl cmulc
            recn divcl 2times divcan2 eqtr3 syl6breq ) ACIJZKLZDMNJZOPCBIJZKLZV
            AOPQZABIJZKLZVAVARJZDOVDUTVCRJZVGOPZVFVGOPZUTVCVAVAUSACEGSTZVBCBGFS
            TZDMHUAMUAUBUCZUDZVNUEVFVHUFPVIVJABCEFGUGVFVHVGVEABEFSTUTVCVKVLUHVA
            VAVNVNUHUIUJUKMVAULJVGDVADMDHUMZMUAUMZVMUNUOMDVPVOVMUPUQUR $.
            $([2-Oct-99]$)
        $}
      $}
    $}
  $}

  $( A non-negative number is its own absolute value. $)
  absidt $p |- ( A e. RR -> ( 0_10 <_ A -> ( abs ` A ) = A ) ) $=
    ( cr wcel cn0 cle wbr cabs cfv wceq wi cded breq2 fveq2 id cleq12d bi2imd
    ax0re elimel absid dedth ) ABCZDAEFZAGHZAIZJDUAADKZEFZUEGHZUEIZJADAUEIZUBUF
    UDUHAUEDELUIUCUGAUEAUEGMUINOPUEADBQRST $.
    $([11-Oct-99]$)

  $( The absolute value of a non-zero number is positive. $)
  absgt0t $p |- ( A e. CC -> ( -. A = 0_10 <-> 0_10 < ( abs ` A ) ) ) $=
    ( cc wcel cn0 wceq wn cabs cfv clt wbr wb cded cleq1 binegd fveq2 breq2d
    bi2bid 0cn elimel absgt0 dedth ) ABCZADEZFZDAGHZIJZKUBADLZDEZFZDUGGHZIJZKAD
    AUGEZUDUIUFUKULUCUHAUGDMNULUEUJDIAUGGOPQUGADBRSTUA $.
    $([1-Oct-99]$)

  $( Swapping order of subtraction doesn't change the absolute value. $)
  abssubt $p |- ( ( A e. CC /\ B e. CC ) ->
                 ( abs ` ( A - B ) ) = ( abs ` ( B - A ) ) ) $=
    ( cc wcel cmin co cabs cfv wceq cn0 cded opreq1 fveq2d opreq2 cleq12d 0cn
    elimel abssub dedth2h ) ACDZBCDZABEFZGHZBAEFZGHZITAJKZBEFZGHZBUFEFZGHZIUFUA
    BJKZEFZGHZUKUFEFZGHZIABJJAUFIZUCUHUEUJUPUBUGGAUFBELMUPUDUIGAUFBENMOBUKIZUHU
    MUJUOUQUGULGBUKUFENMUQUIUNGBUKUFELMOUFUKAJCPQBJCPQRS $.
    $([1-Oct-99]$)

  $( Lemma involving absolute value of differences. $)
  abs3lemt $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\
                   D e. RR ) ) -> (
          ( ( abs ` ( A - C ) ) < ( D / 2_10 ) /\
            ( abs ` ( C - B ) ) < ( D / 2_10 ) ) ->
            ( abs ` ( A - B ) ) < D ) ) $=
    ( cc wcel cr cmin co cabs cfv cl2 cdiv clt wbr wa wi cn0 cded wceq opreq1
    fveq2d breq1d birand bi2imd opreq2 biland bi2and birimd breq2d breq2 0cn
    elimel ax0re abs3lem dedth4h ) AEFZBEFZCEFZDGFZACHIZJKZDLMIZNOZCBHIZJKZVCNO
    ZPZABHIZJKZDNOZQABCDRRUQARSZCHIZJKZVCNOZVGPZVLBHIZJKZDNOZQVOCURBRSZHIZJKZVC
    NOZPZVLVTHIZJKZDNOZQVLUSCRSZHIZJKZVCNOZWHVTHIZJKZVCNOZPZWGQWJUTDRSZLMIZNOZW
    MWQNOZPZWFWPNOZQRRAVLTZVHVPVKVSXBVDVOVGXBVBVNVCNXBVAVMJAVLCHUAUBUCUDXBVJVRD
    NXBVIVQJAVLBHUAUBUCUEBVTTZVPWDVSWGXCVGWCVOXCVFWBVCNXCVEWAJBVTCHUFUBUCUGXCVR
    WFDNXCVQWEJBVTVLHUFUBUCUECWHTZWDWOWGXDVOWKWCWNXDVNWJVCNXDVMWIJCWHVLHUFUBUCX
    DWBWMVCNXDWAWLJCWHVTHUAUBUCUHUIDWPTZWOWTWGXAXEWKWRWNWSXEVCWQWJNDWPLMUAZUJXE
    VCWQWMNXFUJUHDWPWFNUKUEVLVTWHWPAREULUMBREULUMCREULUMDRGUNUMUOUP $.
    $([2-Oct-99]$)

  ${
    abslem2.1 $e |- A e. CC $.
    ${
      abslem2i.2 $e |- A =/= 0_10 $.
      $( Lemma involving absolute values. $)
      abslem2i $p |- ( ( ( * ` ( A / ( abs ` A ) ) ) x. A ) +
      ( ( A / ( abs ` A ) ) x. ( * ` A ) ) ) = ( 2_10 x. ( abs ` A ) ) $=
        ( cl2 cabs cfv cmulc co caddc cdiv ccj abscl recn 2times cr wcel wceq
        cjre mpbi cexp sqval absvalsq eqtr3 opreq1i cn0 wne wn abs00 bineg
        df-ne 3bitr4 mpbir divcan4 cjcl div23 3eqtr3 fveq2i divcl cjmul cjcj
        opreq2i 3eqtr opreq12i eqtr2 ) DAEFZGHVEVEIHAVEJHZKFZAGHZVFAKFZGHZIHVEV
        EABLZMZNVEVHVEVJIVEVJKFZVGVIKFZGHVHVEKFZVEVMVEOPVOVEQVKVEVLRSVEVJKVEVEG
        HZVEJHAVIGHZVEJHVEVJVPVQVEJVEDTHVPVQVEVLUAABUBUCUDVEVEVLVLVEUEUFZAUEUFZ
        CVEUEQZUGAUEQZUGVRVSVTWAABUHUIVEUEUJAUEUJUKULZUMAVIVEBABUNZVLWBUOUPZUQU
        CVFVIAVEBVLWBURWCUSVNAVGGABUTVAVBWDVCVD $.
        $([11-Oct-99]$)
    $}

    $( Lemma involving absolute values. $)
    abslem2 $p |- ( A =/= 0_10 -> ( ( ( * ` ( A / ( abs ` A ) ) ) x. A ) +
      ( ( A / ( abs ` A ) ) x. ( * ` A ) ) ) = ( 2_10 x. ( abs ` A ) ) ) $=
      ( cn0 wne cabs cfv cdiv co ccj cmulc caddc cl2 wceq cn1 cded id fveq2
      opreq12d fveq2d opreq2d cleq12d cc 1cn keepel elimne0 abslem2i dedth ) AC
      DZAAEFZGHZIFZAJHZUJAIFZJHZKHZLUIJHZMUHANOZUQEFZGHZIFZUQJHZUSUQIFZJHZKHZLU
      RJHZMANAUQMZUOVDUPVEVFULVAUNVCKVFUKUTAUQJVFUJUSIVFAUQUIURGVFPZAUQEQZRZSVG
      RVFUJUSUMVBJVIAUQIQRRVFUIURLJVHTUAUQUHANUBBUCUDAUEUFUG $.
      $([11-Oct-99]$)
  $}

