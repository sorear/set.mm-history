$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Rational numbers (as a subset of complex numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( Define the set of rationals.  Definition (rational numbers) of [Apostol]
       p. 22. $)
    df-q $a |- QQ = { x | E. y e. ZZ E. z e. NN x = ( y / z ) } $.
  $}

  ${
    $d x y z A $.
    $( Membership in the set of rationals. $)
    elq $p |- ( A e. QQ <-> E. x e. ZZ E. y e. NN A = ( x / y ) ) $=
      ( vz cq wcel cv cdiv co wceq cn wrex cz cab df-q eleq2i cvv wi oprex
      eleq1 mpbiri a1i r19.23aiv cleq1 birexdv elab2g bitr ) CEFCDGZAGZBGZHIZJZ
      BKLZAMLZDNZFCUKJZBKLZAMLZEUOCDABOPUNURDCUQCQFZAMUQUSRUIMFUPUSBKUPUSRUJKFU
      PUSUKQFUIUJHSCUKQTUAUBUCUBUCUHCJZUMUQAMUTULUPBKUHCUKUDUEUEUFUG $.
      $([8-Jan-02]$)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( The ratio of an integer and a natural number is a rational number. $)
    znq $p |- ( ( A e. ZZ /\ B e. NN ) -> ( A / B ) e. QQ ) $=
      ( vx vy cz wcel cn wa cdiv co cv wceq wrex cq cleqid opreq1 cleq2d
      opreq2 rcla42ev mpan2 elq sylibr ) AEFBGFHZABIJZCKZDKZIJZLZDGMCEMZUDNFUCU
      DUDLZUIUDOUHUJUDAUFIJZLCDABEGUEALUGUKUDUEAUFIPQUFBLUKUDUDUFBAIRQSTCDUDUAU
      B $.
      $([12-Jan-02]$)
  $}

  ${
    $d x y A $.
    $( A rational number is a real number. $)
    qre $p |- ( A e. QQ -> A e. RR ) $=
      ( vx vy cq wcel cv cdiv co wceq cn wrex cz cr elq wi wa eleq1 cc0 wne
      divrclt exp imp32 zr nnret nnne0t jca syl2an syl5bir comm r19.23adv
      r19.23aiv sylbi ) ADEABFZCFZGHZIZCJKZBLKAMEZBCANUQURBLUMLEZUPURCJUSUNJEZU
      PUROUPUSUTPZURUPURUOMEZVAAUOMQUMMEZUNMEZUNRSZPVBUSUTVCVDVEVBVCVDVEVBOUMUN
      TUAUBUMUCUTVDVEUNUDUNUEUFUGUHUIUAUJUKUL $.
      $([14-Nov-02]$)

    $( An integer is a rational number. $)
    zq $p |- ( A e. ZZ -> A e. QQ ) $=
      ( vx vy cv wceq cz wrex cdiv co cn wcel cq c1 cr cc zr recnt div1t 3syl
      cleq2d cleqcom syl6rbbr peano1c opreq2 rcla4ev mpan syl6bi r19.22i
      risset elq 3imtr4 ) BDZAEZBFGAULCDZHIZEZCJGZBFGAFKALKUMUQBFULFKZUMAULMHIZ
      EZUQURUTAULEUMURUSULAURULNKULOKUSULEULPULQULRSTULAUAUBMJKUTUQUCUPUTCMJUNM
      EUOUSAUNMULHUDTUEUFUGUHBAFUIBCAUJUK $.
      $([9-Jan-02]$)
  $}

  $( Integers are a subset of rationals. $)
  zssq $p |- ZZ (_ QQ $=
    ( vx cz cq cv zq ssriv ) ABCADEF $.
    $([9-Jan-02]$)

  $( Rationals are a subset of reals. $)
  qssre $p |- QQ (_ RR $=
    ( vx cq cr cv qre ssriv ) ABCADEF $.
    $([9-Jan-02]$)

