$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Irrationality of square root of 2
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    sqr2irrlem1.1 $e |- A e. NN $.
    sqr2irrlem1.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2.  Technical lemma used
       to simplify the main induction step. $)
    sqr2irrlem1 $p |- ( ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ->
                      ( ( B < A /\ ( A / 2 ) e. NN ) /\
                     ( B ^ 2 ) = ( 2 x. ( ( A / 2 ) ^ 2 ) ) ) ) $=
      ( c2 cexp co cmulc wceq clt wbr cdiv cn wcel wa c1 nnre sqrecl recn
      mulid2 caddc ax1re ltplus1 df-2 breqtrr 2re nnsqcl nngt0 ltmul1i mpbi
      eqbrtrr breq2 mpbiri cc0 cle wb ax0re ltlei lt2sqe mp2an sylibr 2pos
      gt0ne0i divmul eleq1 nnesq sylbir cleqcoms jca divrcl mulrcl mulcan nncn
      sqdiv sqval opreq2i eqtr mulass mulcl muln0 divcan2 3eqtr3 cleq1i bitr3
      biimpr cleqcomd ) AEFGZEBEFGZHGZIZBAJKZAELGZMNZOWHEWLEFGZHGZIWJWKWMWJWHWG
      JKZWKWJWPWHWIJKPWHHGZWHWIJWHWHBBDQZRZSZTPEJKWQWIJKPPPUAGEJPUBUCUDUEPEWHUB
      UFWSWHBDUGZUHUIUJUKWGWIWHJULUMUNBUOKUNAUOKWKWPUPUNBUQWRBDUHURUNAUQACQZACU
      HURBAWRXBUSUTVAWMWIWGWIWGIWGELGZWHIZWMWGEWHWGAXBRSZEUFSZWTEUFVBVCZVDXDXCM
      NZWMXDXHWHMNXAXCWHMVEUMACVFVAVGVHVIWJWOWHWOWHIZWJXIEWOHGZWIIWJEWOWHXFWOEW
      NUFWLAEXBUFXGVJRZVKSWTXGVLXJWGWIEEHGZWNHGXLWGXLLGZHGXJWGWNXMXLHWNWGEEFGZL
      GXMAEACVMXFXGVNXNXLWGLEXFVOVPVQVPEEWNXFXFWNXKSVRXLWGEEXFXFVSXEEEXFXFXGXGV
      TWAWBWCWDWEWFVI $.
      $([20-Aug-01]$)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminates hypotheses with
     weak deduction theorem. $)
  sqr2irrlem2 $p |- ( ( A e. NN /\ B e. NN ) ->
                   ( ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ->
                    ( ( B < A /\ ( A / 2 ) e. NN ) /\
                   ( B ^ 2 ) = ( 2 x. ( ( A / 2 ) ^ 2 ) ) ) ) ) $=
    ( cn wcel c2 cexp co cmulc wceq clt wbr cdiv wa wi cded opreq1 cleq1d
    breq2 eleq1d bi2and opreq1d opreq2d cleq2d bi2imd breq1 birand 2nn elimel
    sqr2irrlem1 dedth2h ) ACDZBCDZAEFGZEBEFGZHGZIZBAJKZAELGZCDZMZUNEUREFGZHGZIZ
    MZNUKAEOZEFGZUOIZBVEJKZVEELGZCDZMZUNEVIEFGZHGZIZMZNVFEULBEOZEFGZHGZIZVPVEJK
    ZVJMZVQVMIZMZNABEEAVEIZUPVGVDVOWDUMVFUOAVEEFPQWDUTVKVCVNWDUQVHUSVJAVEBJRWDU
    RVICAVEELPZSTWDVBVMUNWDVAVLEHWDURVIEFWEUAUBUCTUDBVPIZVGVSVOWCWFUOVRVFWFUNVQ
    EHBVPEFPZUBUCWFVKWAVNWBWFVHVTVJBVPVEJUEUFWFUNVQVMWGQTUDVEVPAECUGUHBECUGUHUI
    UJ $.
    $([20-Aug-01]$)

  ${
    $d x y z w $.
    $( Main theorem for irrationality of square root of 2.  There are no
       natural numbers such that the square of one is twice the square of the
       other.  Uses strong induction. $)
    sqr2irrlem3 $p |- -. E. x e. NN E. y e. NN
                            ( x ^ 2 ) = ( 2 x. ( y ^ 2 ) ) $=
      ( vz vw cv c2 cexp co cmulc wceq cn wrex wn wral wcel weq opreq1 cleq1d
      binegd biraldv opreq2d cleq2d cbvralv syl6bb clt wbr wi wa cdiv breq1
      bi2imd rcla4v imp syl6 imp3a imnan sylib adantll sqr2irrlem2 adantlr
      mtod exp31 r19.21adv indstr ralnex rgen mpbi ) AEZFGHZFBEZFGHZIHZJZBKLZMZ
      AKNVNAKLMVOAKVHKOZVMMZBKNZVOVRCEZFGHZFDEZFGHZIHZJZMZDKNZACACPZVRVTVLJZMZB
      KNWFWGVQWIBKWGVMWHWGVIVTVLVHVSFGQRSTWIWEBDKBDPZWHWDWJVLWCVTWJVKWBFIVJWAFG
      QUAUBSUCUDVPVSVHUEUFZWFUGZCKNZVQBKVPWMVJKOZVQVPWMUHWNUHVMVJVHUEUFZVHFUIHZ
      KOZUHZVKFWPFGHZIHZJZUHZWMWNXBMZVPWMWNUHZWRXAMZUGXCXDWOWQXEXDWOVKWCJZMZDKN
      ZWQXEUGWMWNWOXHUGZWLXICVJKCBPZWKWOWFXHVSVJVHUEUJXJWEXGDKXJWDXFXJVTVKWCVSV
      JFGQRSTUKULUMXGXEDWPKWAWPJZXFXAXKWCWTVKXKWBWSFIWAWPFGQUAUBSULUNUOWRXAUPUQ
      URVPWNVMXBUGWMVHVJUSUTVAVBVCVDVMBKVEUQVFVNAKVEVG $.
      $([20-Aug-01]$)
  $}

  ${
    sqr2irrlem4.1 $e |- A e. NN $.
    sqr2irrlem4.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2. $)
    sqr2irrlem4 $p |- ( ( sqr ` 2 ) = ( A / B ) <->
                      ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ) $=
      ( c2 cexp co cmulc wceq csqr cfv cdiv nncn nnne0 sqdiv cleqcomi nnsqcl
      2re recn divcan4 cleq12i nnre sqrecl mulrcl div11 cleqcom 3bitr3 cc0 cle
      wbr wb ax0re 2pos ltlei divrcl sqege0 sqr11 mp2an bitr4 nngt0 divgt0i
      sqrsqe ax-mp cleq2i bitr2 ) AEFGZEBEFGZHGZIZEJKZABLGZEFGZJKZIZVJVKIVIEVLI
      ZVNVFVGLGZVHVGLGZIVLEIVIVOVPVLVQEVLVPABACMBDMBDNZOPVGEVGBDQZMZERSVGVSNZTU
      AVFVHVGVFAACUBZUCSVHEVGRVGVSUBUDSVTWAUEVLEUFUGUHEUIUJUHVLUIUJVNVOUKUHEULR
      UMUNVKABWBBDUBZVRUOZUPEVLRVKWDUCUQURUSVMVKVJUHVKUIUJVMVKIUHVKULWDABWBWCAC
      UTBDUTVAUNVKWDVBVCVDVE $.
      $([20-Aug-01]$)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminates hypotheses with
     weak deduction theorem. $)
  sqr2irrlem5 $p |- ( ( A e. NN /\ B e. NN ) -> ( ( sqr ` 2 ) = ( A / B )
                     <-> ( A ^ 2 ) = ( 2 x. ( B ^ 2 ) ) ) ) $=
    ( cn wcel c2 csqr cfv cdiv co wceq cexp cmulc wb cded opreq1 cleq2d cleq1d
    bi2bid opreq2 opreq2d 2nn elimel sqr2irrlem4 dedth2h ) ACDZBCDZEFGZABHIZJZA
    EKIZEBEKIZLIZJZMUGUEAENZBHIZJZUNEKIZULJZMUGUNUFBENZHIZJZUQEUSEKIZLIZJZMABEE
    AUNJZUIUPUMURVEUHUOUGAUNBHOPVEUJUQULAUNEKOQRBUSJZUPVAURVDVFUOUTUGBUSUNHSPVF
    ULVCUQVFUKVBELBUSEKOTPRUNUSAECUAUBBECUAUBUCUD $.
    $([20-Aug-01]$)

  ${
    $d x y $.
    $( The square root of 2 is irrational. $)
    sqr2irr $p |- ( sqr ` 2 ) e/ QQ  $=
      ( vx vy c2 csqr cfv cq wnel wcel wn cv cdiv co wceq cn wrex cz cexp
      cmulc sqr2irrlem3 sqr2irrlem5 birexdva birexa mtbir wa wex cc0 clt wbr
      wi wb cr ax0re ltdivmult mp3an1 zr nnret syl2an nngt0t syl5 comm anabsi7
      2re 2pos sqrgt0i breq2 mpbii syl5bi cc nncnt mulzer2t syl breq1d adantl
      sylibd exp r19.23adv anc2li eln syl6ibr impac 19.22i df-rex 3imtr4 mto
      elq df-nel mpbir ) CDEZFGWHFHZIWIWHAJZBJZKLZMZBNOZAPOZWOWNANOZWPWJCQLCWKC
      QLRLMZBNOZANOABSWNWRANWJNHZWMWQBNWJWKTUAUBUCWJPHZWNUDZAUEWSWNUDZAUEWOWPXA
      XBAWTWNWSWTWNWTUFWJUGUHZUDWSWTWNXCWTWMXCBNWTWKNHZWMXCUIWTXDUDZWMUFWKRLZWJ
      UGUHZXCXEUFWLUGUHZXGWMWTXDXHXGUJZXEXDXIXEUFWKUGUHZXIXDWJUKHZWKUKHZXJXIUIZ
      WTXDUFUKHXKXLXMULUFWJWKUMUNWJUOWKUPUQWKURUSUTVAWMUFWHUGUHXHCVBVCVDWHWLUFU
      GVEVFVGXDXGXCUJWTXDXFUFWJUGXDWKVHHXFUFMWKVIWKVJVKVLVMVNVOVPVQWJVRVSVTWAWN
      APWBWNANWBWCWDABWHWEUCWHFWFWG $.
      $([8-Jan-02]$)
  $}

  ${
    $( The sequence ` NN ` , ` ZZ ` , ` QQ ` , ` RR ` , and ` CC ` forms
       a chain of proper subsets.  In each case the proper subset
       relationship is shown by demonstrating a number that belongs to
       one set but not the other.  We show that zero belongs to ` ZZ `
       but not ` NN ` , one-half belongs to ` QQ ` but not ` ZZ ` ,
       the square root of 2 belongs to ` RR ` but not ` QQ ` , and finally
       that the imaginary number ` i ` belongs to ` CC ` but not ` RR ` . $)
    nthruc $p |- ( ( NN (. ZZ /\ ZZ (. QQ ) /\ ( QQ (. RR /\ RR (. CC ) ) $=
      ( cn cz wpss cq wa cr cc wss cc0 wcel wn nnssz 0z 0nnn pm3.2i ssnelpss
      mp2 c1 c2 cdiv co zssq peano1c ssii 2nn znq mp2an wceq cneg w3o wo wne
      1cn 2re recn ax1ne0 2pos gt0ne0i divneq0 df-ne mpbi cle wbr clt caddc
      df-2 opreq2i lt01 ax1re halfpos eqbrtr divrcl lelt bicon2 nnge1t mto
      pm3.2ni ax0re recgt0i ltlei wb le0neg2t ax-mp negrcl nngt0t df-3or mtbir
      intnan elz csqr cfv qssre sqrlem24 wnel sqr2irr df-nel ci axresscn axicn
      inelr ) ABCZBDCZEDFCZFGCZEXAXBABHIBJZIAJKZEXALXEXFMNOABIPQBDHRSTUAZDJZXGB
      JZKZEXBUBXHXJRBJSAJXHABRLUCUDUERSUFUGXIXGFJZXGIUHZXGAJZXGUIZAJZUJZEXPXKXP
      XLXMUKZXOUKXQXOXLXMXGIULXLKRSUMSUNUOUPSUNUQURZUSXGIUTVAXMRXGVBVCZXGRVDVCZ
      XSKXGRRRVEUAZTUAZRVDSYARTVFVGIRVDVCYBRVDVCVHRVIVJVAVKXSXTRXGVIRSVIUNXRVLZ
      VMVNVAXGVOVPVQXOIXNVDVCZXNIVBVCZYDKIXGVBVCZYEIXGVRYCSUNUQVSVTXKYFYEWAYCXG
      WBWCVAXNIXGYCWDVRVMVAXNWEVPVQXLXMXOWFWGWHXGWIWGOBDXGPQOXCXDDFHSWJWKZFJZYG
      DJKZEXCWLYHYISUNUQWMYGDWNYIWOYGDWPVAODFYGPQFGHWQGJZWQFJKZEXDWRYJYKWSWTOFG
      WQPQOO $.
      $([12-Jan-02]$)
  $}

  ${
    $( The sequence ` NN ` , ` NN0 ` , and ` ZZ ` forms a chain of proper
       subsets.  In each case the proper subset relationship is shown by
       demonstrating a number that belongs to one set but not the other.  We
       show that zero belongs to ` NN0 ` but not ` NN ` and minus one belongs
       to ` ZZ ` but not ` NN0 ` . This theorem refines the chain of proper
       subsets ~ nthruc . $)
    nthruz $p |- ( NN (. NN0 /\ NN0 (. ZZ ) $=
      ( cn cn0 wpss cz wss cc0 wcel wn wa nnssnn0 0nn0 0nnn pm3.2i ssnelpss
      mp2 c1 cneg nn0ssz peano1c nnnz ax-mp cr clt wbr ax1re negrcl neg0 lt01
      eqbrtr ax0re ltnegcon1 mpbir lt0nnn0 mp2an ) ABCZBDCZABEFBGZFAGHZIUOJUQUR
      KLMABFNOBDEPQZDGZUSBGHZIUPRUTVAPAGUTSPTUAUSUBGUSFUCUDZVAPUEUFVBFQZPUCUDVC
      FPUCUGUHUIPFUEUJUKULUSUMUNMBDUSNOM $.
      $([?]$)
  $}


