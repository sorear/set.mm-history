$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Irrationality of square root of 2
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    sqr2irrlem1.1 $e |- A e. NN $.
    sqr2irrlem1.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2.  Technical lemma used
       to simplify the main induction step.  Note that "2_10"  just means
       2 base 10. $)
    sqr2irrlem1 $p |- ( ( A ^ 2_10 ) = ( 2_10 x. ( B ^ 2_10 ) ) ->
                      ( ( B < A /\ ( A / 2_10 ) e. NN ) /\
                     ( B ^ 2_10 ) = ( 2_10 x. ( ( A / 2_10 ) ^ 2_10 ) ) ) ) $=
      ( cl2 cexp co cmulc wceq clt wbr cdiv cn wcel wa cn1 nnre sqrecl recn
      mulid2 caddc ax1re ltplus1 df-2 breqtrr 2re nnsqcl nngt0 ltmul1i mpbi
      eqbrtrr breq2 mpbiri cn0 cle wb ax0re ltlei lt2sqe mp2an sylibr 2pos
      gt0ne0i divmul eleq1 nnesq sylbir cleqcoms jca divrcl mulrcl mulcanb
      sqdiv sqval opreq2i eqtr mulass mulcl muln0 divcan2 3eqtr3 cleq1i bitr3
      biimpr cleqcomd ) AEFGZEBEFGZHGZIZBAJKZAELGZMNZOWGEWKEFGZHGZIWIWJWLWIWGWF
      JKZWJWIWOWGWHJKPWGHGZWGWHJWGWGBBDQZRZSZTPEJKWPWHJKPPPUAGEJPUBUCUDUEPEWGUB
      UFWRWGBDUGZUHUIUJUKWFWHWGJULUMUNBUOKUNAUOKWJWOUPUNBUQWQBDUHURUNAUQACQZACU
      HURBAWQXAUSUTVAWLWHWFWHWFIWFELGZWGIZWLWFEWGWFAXARSZEUFSZWSEUFVBVCZVDXCXBM
      NZWLXCXGWGMNWTXBWGMVEUMACVFVAVGVHVIWIWNWGWNWGIZWIXHEWNHGZWHIWIEWNWGXEWNEW
      MUFWKAEXAUFXFVJRZVKSWSXFVLXIWFWHEEHGZWMHGXKWFXKLGZHGXIWFWMXLXKHWMWFEEFGZL
      GXLAEAXASXEXFVMXMXKWFLEXEVNVOVPVOEEWMXEXEWMXJSVQXKWFEEXEXEVRXDEEXEXEXFXFV
      SVTWAWBWCWDWEVI $.
      $([20-Aug-01]$)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminate hypotheses with
     deduction theorem. $)
  sqr2irrlem2 $p |- ( ( A e. NN /\ B e. NN ) ->
                   ( ( A ^ 2_10 ) = ( 2_10 x. ( B ^ 2_10 ) ) ->
                    ( ( B < A /\ ( A / 2_10 ) e. NN ) /\
                   ( B ^ 2_10 ) = ( 2_10 x. ( ( A / 2_10 ) ^ 2_10 ) ) ) ) ) $=
    ( cn wcel cl2 cexp co cmulc wceq clt wbr cdiv wa wi cded opreq1 cleq1d
    breq2 eleq1d bi2and opreq1d opreq2d cleq2d bi2imd breq1 birand 2nn elimel
    sqr2irrlem1 dedth2h ) ACDZBCDZAEFGZEBEFGZHGZIZBAJKZAELGZCDZMZUNEUREFGZHGZIZ
    MZNUKAEOZEFGZUOIZBVEJKZVEELGZCDZMZUNEVIEFGZHGZIZMZNVFEULBEOZEFGZHGZIZVPVEJK
    ZVJMZVQVMIZMZNABEEAVEIZUPVGVDVOWDUMVFUOAVEEFPQWDUTVKVCVNWDUQVHUSVJAVEBJRWDU
    RVICAVEELPZSTWDVBVMUNWDVAVLEHWDURVIEFWEUAUBUCTUDBVPIZVGVSVOWCWFUOVRVFWFUNVQ
    EHBVPEFPZUBUCWFVKWAVNWBWFVHVTVJBVPVEJUEUFWFUNVQVMWGQTUDVEVPAECUGUHBECUGUHUI
    UJ $.
    $([20-Aug-01]$)

  ${
    $d x y z w $.
    $( Main theorem for irrationality of square root of 2.  There are no
       natural numbers such that the square of one is twice the square of the
       other.  Uses strong induction. $)
    sqr2irrlem3 $p |- -. E. x e. NN E. y e. NN
                            ( x ^ 2_10 ) = ( 2_10 x. ( y ^ 2_10 ) ) $=
      ( vz vw cv cl2 cexp co cmulc wceq cn wrex wn wral wcel weq opreq1 cleq1d
      binegd biraldv opreq2d cleq2d rcbvalv syl6bb clt wbr wi wa cdiv breq1
      bi2imd rcla4v imp syl6 imp3a imnan sylib adantall sqr2irrlem2 adantalr
      mtod exp31 r19.21adv indstr ralnex rgen mpbi ) AEZFGHZFBEZFGHZIHZJZBKLZMZ
      AKNVNAKLMVOAKVHKOZVMMZBKNZVOVRCEZFGHZFDEZFGHZIHZJZMZDKNZACACPZVRVTVLJZMZB
      KNWFWGVQWIBKWGVMWHWGVIVTVLVHVSFGQRSTWIWEBDKBDPZWHWDWJVLWCVTWJVKWBFIVJWAFG
      QUAUBSUCUDVPVSVHUEUFZWFUGZCKNZVQBKVPWMVJKOZVQVPWMUHWNUHVMVJVHUEUFZVHFUIHZ
      KOZUHZVKFWPFGHZIHZJZUHZWMWNXBMZVPWMWNUHZWRXAMZUGXCXDWOWQXEXDWOVKWCJZMZDKN
      ZWQXEUGWMWNWOXHUGZWLXICVJKCBPZWKWOWFXHVSVJVHUEUJXJWEXGDKXJWDXFXJVTVKWCVSV
      JFGQRSTUKULUMXGXEDWPKWAWPJZXFXAXKWCWTVKXKWBWSFIWAWPFGQUAUBSULUNUOWRXAUPUQ
      URVPWNVMXBUGWMVHVJUSUTVAVBVCVDVMBKVEUQVFVNAKVEVG $.
      $([20-Aug-01]$)
  $}

  ${
    sqr2irrlem4.1 $e |- A e. NN $.
    sqr2irrlem4.2 $e |- B e. NN $.
    $( Lemma for irrationality of square root of 2. $)
    sqr2irrlem4 $p |- ( ( sqr ` 2_10 ) = ( A / B ) <->
                      ( A ^ 2_10 ) = ( 2_10 x. ( B ^ 2_10 ) ) ) $=
      ( cl2 cexp co cmulc wceq csqr cfv cdiv nnre recn nnne0 sqdiv cleqcomi
      nnsqcl 2re divcan4 cleq12i sqrecl mulrcl div11 cleqcom 3bitr3 cn0 cle
      wbr wb ax0re 2pos ltlei divrcl sqege0 sqr11 mp2an bitr4 nngt0 divgt0i
      sqrsqe ax-mp cleq2i bitr2 ) AEFGZEBEFGZHGZIZEJKZABLGZEFGZJKZIZVIVJIVHEVKI
      ZVMVEVFLGZVGVFLGZIVKEIVHVNVOVKVPEVKVOABAACMZNBBDMZNBDOZPQVFEVFVFBDRZMZNZE
      SNVFVTOZTUAVEVGVFVEAVQUBNVGEVFSWAUCNWBWCUDVKEUEUFUGEUHUIUGVKUHUIVMVNUJUGE
      UKSULUMVJABVQVRVSUNZUOEVKSVJWDUBUPUQURVLVJVIUGVJUHUIVLVJIUGVJUKWDABVQVRAC
      USBDUSUTUMVJWDVAVBVCVD $.
      $([20-Aug-01]$)
  $}

  $( Lemma for irrationality of square root of 2.  Eliminate hypotheses with
     deduction theorem. $)
  sqr2irrlem5 $p |- ( ( A e. NN /\ B e. NN ) -> ( ( sqr ` 2_10 ) = ( A / B )
                     <-> ( A ^ 2_10 ) = ( 2_10 x. ( B ^ 2_10 ) ) ) ) $=
    ( cn wcel cl2 csqr cfv cdiv co wceq cexp cmulc wb cded opreq1 cleq2d
    cleq1d bi2bid opreq2 opreq2d 2nn elimel sqr2irrlem4 dedth2h ) ACDZBCDZEFGZA
    BHIZJZAEKIZEBEKIZLIZJZMUGUEAENZBHIZJZUNEKIZULJZMUGUNUFBENZHIZJZUQEUSEKIZLIZ
    JZMABEEAUNJZUIUPUMURVEUHUOUGAUNBHOPVEUJUQULAUNEKOQRBUSJZUPVAURVDVFUOUTUGBUS
    UNHSPVFULVCUQVFUKVBELBUSEKOTPRUNUSAECUAUBBECUAUBUCUD $.
    $([20-Aug-01]$)

  ${
    $d x y $.
    $( Irrationality of square root of 2. $)
    sqr2irr $p |- ( sqr ` 2_10 ) e/ QQ  $=
      ( vx vy cl2 csqr cfv cq wnel wcel wn cr cn0 wceq cabs cv cdiv co cn wrex
      wo wa wne 2re 2pos sqrlem24 sqrgt0i gt0ne0i df-ne mpbi cexp cmulc
      sqr2irrlem3 sqr2irrlem5 cle wbr ax0re ltlei absid ax-mp cleq1i syl5bb
      birexdva birexa mtbir pm3.2i ioran mpbir intnan elq df-nel ) CDEZFGVJFHZI
      VKVJJHZVJKLZVJMEZANZBNZOPZLZBQRZAQRZSZTWAVLWAIVMIZVTIZTWBWCVJKUAWBVJCUBUC
      UDZCUBUCUEZUFVJKUGUHVTVOCUIPCVPCUIPUJPLZBQRZAQRABUKVSWGAQVOQHZVRWFBQWHVPQ
      HTVJVQLWFVRVOVPULVNVJVQKVJUMUNVNVJLKVJUOWDWEUPVJWDUQURUSUTVAVBVCVDVMVTVEV
      FVGABVJVHVCVJFVIVF $.
      $([20-Aug-01]$)
  $}

