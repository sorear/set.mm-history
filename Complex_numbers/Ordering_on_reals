$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Ordering on reals
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define "less than or equal to" on real subset of compex numbers. $)
  df-le $a |- <_ = ( ( RR X. RR ) \ `' < ) $.

  $( "L.e." expressed in terms of "less than". $)
  leltt $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B <-> -. B < A ) ) $=
    ( cr wcel wa cle wbr cop clt ccnv wn cxp wb opelxpi cdif df-le eleq2i
    eldif bitr baib syl df-br syl5bb opelcnvg syl6rbbr binegd bitr4d ) ACDBCDEZ
    ABFGZABHZIJZDZKZBAIGZKUHUJFDZUMUIUHUJCCLZDZUOUMMABCCNUOUQUMUOUJUPUKOZDUQUME
    FURUJPQUJUPUKRSTUAABFUBUCUHUNULUHULBAHIDUNABCCIUDBAIUBUEUFUG $.
    $([13-May-99]$)

  ${
    $d x y z $. $( ` x y z ` are dummy. $)
    $( "Less than" is a strict ordering. $)
    ltso $p |- < Or RR $=
      ( vx vy vz cr clt cv wcel w3a wbr weq wo wn wb wa wi axlttri 3adant3
      axlttrn jca so ) ABCDEAFZDGZBFZDGZCFZDGZHUAUCEIZABJUCUAEIKLMZUGUCUEEINUAU
      EEIOUBUDUHUFUAUCPQUAUCUERST $.
      $([19-Jan-97]$)
  $}

  $( Trichotomy law. $)
  lttri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( -. A < B /\ -. B < A ) ) ) $=
    ( cr clt wor wcel wa wceq wbr wn wb sotrieq2 ltso mpan ) CDEACFBCFGABHABDIJ
    BADIJGKCABDLMN $.
    $([5-May-99]$)

  $( Trichotomy law. $)
  letri3t $p |- ( ( A e. RR /\ B e. RR ) ->
                ( A = B <-> ( A <_ B /\ B <_ A ) ) ) $=
    ( cr wcel wa wceq clt wbr wn cle lttri3t ancom syl6bbr leltt wb ancoms
    bi2and bitr4d ) ACDZBCDZEZABFZBAGHIZABGHIZEZABJHZBAJHZEUAUBUDUCEUEABKUCUDLM
    UAUFUCUGUDABNTSUGUDOBANPQR $.
    $([14-May-99]$)

  $( "L.e." expressed in terms of less than or equals. $)
  leloet $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B <-> ( A < B \/ A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wn wceq wo leltt wb wor sotric ltsor mpan ancoms
    bicon2d cleqcom biror orcom bitr syl5rbbr bitrd ) ACDZBCDZEZABFGBAHGZIZABHG
    ZABJZKZABLUHBAJZUKKZUJUMUHUIUOUGUFUIUOIMZCHNUGUFEUPCBAHOPQRSUOULUKKUMUNULUK
    BATUAULUKUBUCUDUE $.
    $([13-May-99]$)

  $( L.e. implies less than is not equals. $)
  leltne $p |- ( ( A e. RR /\ B e. RR ) ->
               ( A <_ B -> ( A < B <-> -. A = B ) ) ) $=
    ( cr wcel wa cle wbr clt wceq wn wb wi lttri3t pm3.26 syl6bi adantr wo
    leloet biimpa ord impbid bicon2d exp ) ACDBCDEZABFGZABHGZABIZJKUDUEEZUGUFUH
    UGUFJZUDUGUILUEUDUGUIBAHGJZEUIABMUIUJNOPUHUFUGUDUEUFUGQABRSTUAUBUC $.
    $([27-Jul-99]$)

  $( Transitive law. $)
  lelttrt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
             ( ( A <_ B /\ B < C ) -> A < C ) ) $=
    ( cr wcel w3a cle wbr clt wceq wo wi wb leloet 3adant3 axlttrn exp3a breq1
    biimprd a1i jaod sylbid imp3a ) ADEZBDEZCDEZFZABGHZBCIHZACIHZUGUHABIHZABJZK
    ZUIUJLZUDUEUHUMMUFABNOUGUKUNULUGUKUIUJABCPQULUNLUGULUJUIABCIRSTUAUBUC $.
    $([23-May-99]$)

  $( "Less than" is irreflexive. $)
  ltnrt $p |- ( A e. RR -> -. A < A ) $=
    ( cr clt wor wcel wbr wn sonr ltsor mpan ) BCDABEAACFGBACHIJ $.
    $([18-Aug-99]$)

  $( "Less than or equals" is reflexive. $)
  leidt $p |- ( A e. RR -> A <_ A ) $=
    ( cr wcel cle wbr wa clt wceq wo leloet wn cleqid a1i orri mpbiri anabss )
    ABCZAADEZQQFRAAGEZAAHZIAAJSTTSKALMNOP $.
    $([18-Aug-99]$)

  ${
    lt.1 $e |- A e. RR $.

    ${
      lt.2 $e |- B e. RR $.

      $( Consequence of trichotomy. $)
      lttri2 $p |- ( -. A = B <-> ( A < B \/ B < A ) ) $=
        ( clt wbr wo wceq wn cr wor wcel wa wb sotrieq ltso pm3.2i mp2an bicom
        bicon1 ) ABEFBAEFGZABHZUBUAIZJEKAJLZBJLZMUBUCNJABEOPUDUECDQRST $.
        $([19-Jan-97]$)

      $( Consequence of trichotomy. $)
      lttri3 $p |- ( A = B <-> ( -. A < B /\ -. B < A ) ) $=
        ( cr wcel wceq clt wbr wn wa wb lttri3t mp2an ) AEFBEFABGABHIJBAHIJKLAB
        MCDN $.
        $([14-May-99]$)

      $( Consequence of trichotomy. $)
      letri3 $p |- ( A = B <-> ( A <_ B /\ B <_ A ) ) $=
        ( cr wcel wceq cle wbr wa wb letri3t mp2an ) AEFBEFABGABHIBAHIJKABLCDM
        $.
        $([14-May-99]$)

      $( L.e. in terms of l.t. $)
      leloe $p |- ( A <_ B <-> ( A < B \/ A = B ) ) $=
        ( cr wcel cle wbr clt wceq wo wb leloet mp2an ) AEFBEFABGHABIHABJKLABMC
        DN $.
        $([14-May-99]$)

      $( "Less than" is not symmetric. $)
      ltnsym $p |- ( A < B -> -. B < A ) $=
        ( clt wbr wn wi wa imnan cr wor wcel so2nr ltso pm3.2i mp2an mpbir ) AB
        EFZBAEFZGHSTIGZSTJKELAKMZBKMZIUAKABENOUBUCCDPQR $.
        $([6-May-99]$)

      $( L.e. in terms of l.t. $)
      lelt $p |- ( A <_ B <-> -. B < A ) $=
        ( cr wcel cle wbr clt wn wb leltt mp2an ) AEFBEFABGHBAIHJKABLCDM $.
        $([24-May-99]$)

      $( L.t. implies l.e. $)
      ltle $p |- ( A < B -> A <_ B ) $=
        ( clt wbr wn cle ltnsym cr wcel wb leltt mp2an sylibr ) ABEFBAEFGZABHFZ
        ABCDIAJKBJKQPLABMCDNO $.
        $([14-May-99]$)

      ${
        ltlei.1 $e |- A < B $.
        $( L.t. implies l.e. (inference). $)
        ltlei $p |- A <_ B $=
          ( clt wbr cle ltle ax-mp ) ABFGABHGEABCDIJ $.
          $([22-Aug-99]$)
      $}

      $( Equality implies l.e. $)
      eqle $p |- ( A = B -> A <_ B ) $=
        ( wceq cle wbr wa letri3 pm3.26 sylbi ) ABEABFGZBAFGZHLABCDILMJK $.
        $([23-May-99]$)

      $( L.t. implies not equal. $)
      ltneq $p |- ( A < B -> -. A = B ) $=
        ( clt wbr wceq wn wo lttri2 biimpr orci ) ABEFZBAEFZABGHZOMNIABCDJKL $.
        $([28-Jul-99]$)

      $( L.t. implies not equal. $)
      ltne $p |- ( A < B -> A =/= B ) $=
        ( clt wbr wceq wn wne ltneq df-ne sylibr ) ABEFABGHABIABCDJABKL $.
        $([14-May-99]$)

      $( Trichotomy law for l.e. $)
      letri $p |- ( A <_ B \/ B <_ A ) $=
        ( cle wbr wn clt lelt bicon2 ltle sylbir orri ) ABEFZBAEFZNGBAHFZONPABC
        DIJBADCKLM $.
        $([2-Aug-99]$)

      ${
        lt.3 $e |- C e. RR $.
        $( "Less than" is transitive. $)
        lttr $p |- ( ( A < B /\ B < C ) -> A < C ) $=
          ( cr wcel clt wbr wa wi axlttrn mp3an ) AGHBGHCGHABIJBCIJKACIJLABCMDE
          FN $.
          $([14-May-99]$)

        $( L.e., l.t. transitive law. $)
        lelttr $p |- ( ( A <_ B /\ B < C ) -> A < C ) $=
          ( cr wcel cle wbr clt wa wi lelttrt mp3an ) AGHBGHCGHABIJBCKJLACKJMAB
          CNDEFO $.
          $([14-May-99]$)

        $( L.t., l.e. transitive law. $)
        ltletr $p |- ( ( A < B /\ B <_ C ) -> A < C ) $=
          ( clt wbr cle wceq wo lttr exp breq2 biimpcd jaod leloe syl5ib imp )
          ABGHZBCIHZACGHZTBCGHZBCJZKUBUATUCUBUDTUCUBABCDEFLMUDTUBBCAGNOPBCEFQRS
          $.
          $([14-May-99]$)

        $( L.e. is transitive. $)
        letr $p |- ( ( A <_ B /\ B <_ C ) -> A <_ C ) $=
          ( cle wbr clt wceq wo lelttr exp ltle syl6 breq2 biimpcd jaod leloe
          syl5ib imp ) ABGHZBCGHZACGHZUBBCIHZBCJZKUDUCUBUEUDUFUBUEACIHZUDUBUEUG
          ABCDEFLMACDFNOUFUBUDBCAGPQRBCEFSTUA $.
          $([14-May-99]$)

        $( Addition to both sides of an inequality. $)
        ltadd2 $p |- ( A < B <-> ( C + A ) < ( C + B ) ) $=
          ( clt wbr caddc co cr wcel wi axltadd mp3an cneg addrcl negrcl cn0
          recn 0cn negid addcoml opreq1i addass addid2 3eqtr3 3brtr3g impbi )
          ABGHZCAIJZCBIJZGHZAKLBKLCKLUJUMMABCNDEFOUMCPZUKIJZUNULIJZABGUKKLULKLU
          NKLUMUOUPGHMUKULUNNCAFDQCBFEQCFRZOUNCIJZAIJSAIJUOAURSAICUNSCFTZUNUQTZ
          UACUSUBUCZUDUNCAUTUSADTZUEAVBUFUGURBIJSBIJUPBURSBIVAUDUNCBUTUSBETZUEB
          VCUFUGUHUI $.
          $([21-Jan-97]$)

        $( Addition to both sides of an inequality.  Theorem I.18 of Apostol,
           p. 20. $)
        ltadd1 $p |- ( A < B <-> ( A + C ) < ( B + C ) ) $=
          ( clt wbr caddc co ltadd2 recn addcom breq12i bitr ) ABGHCAIJZCBIJZGH
          ACIJZBCIJZGHABCDEFKPRQSGCACFLZADLMCBTBELMNO $.
          $([21-Jan-97]$)

        $( Addition to both sides of an inequality. $)
        leadd1 $p |- ( A <_ B <-> ( A + C ) <_ ( B + C ) ) $=
          ( clt wbr wn caddc co cle ltadd1 bineg lelt addrcl 3bitr4 ) BAGHZIBCJ
          KZACJKZGHZIABLHTSLHRUABACEDFMNABDEOTSACDFPBCEFPOQ $.
          $([11-Aug-99]$)

        $( Addition to both sides of an inequality. $)
        leadd2 $p |- ( A <_ B <-> ( C + A ) <_ ( C + B ) ) $=
          ( cle wbr caddc co leadd1 recn addcom breq12i bitr ) ABGHACIJZBCIJZGH
          CAIJZCBIJZGHABCDEFKPRQSGACADLCFLZMBCBELTMNO $.
          $([11-Aug-99]$)

        $( Inequality relation between subtraction and addition. $)
        ltsubadd $p |- ( ( A - B ) < C <-> A < ( C + B ) ) $=
          ( cmin co clt wbr cneg caddc recn subneg breq1i negrcl addrcl ltadd1
          cn0 ax0re 0cn negid addcoml opreq2i addass1l addid1 eqtr 3bitr ) ABGH
          ZCIJABKZLHZCIJUKBLHZCBLHZIJAUMIJUIUKCIABADMZBEMZNOUKCBAUJDBEPZQFERULA
          UMIULASLHZAAUJBUQUNUJUPMZUOUQASDTQMUJBLHSALBUJSUOURUABUOUBUCUDUEAUNUF
          UGOUH $.
          $([21-Jan-97]$)

        $( Inequality relation between subtraction and addition. $)
        ltsubadd2 $p |- ( ( A - B ) < C <-> A < ( B + C ) ) $=
          ( cmin co clt wbr caddc ltsubadd recn addcom breq2i bitr ) ABGHCIJACB
          KHZIJABCKHZIJABCDEFLQRAICBCFMBEMNOP $.
          $([21-Jan-97]$)

        $( Inequality relation between subtraction and addition. $)
        lesubadd2 $p |- ( ( A - B ) <_ C <-> A <_ ( B + C ) ) $=
          ( cmin co clt wbr wceq wo caddc cle ltsubadd2 recn subadd cleqcom
          bitr bi2or subrcl leloe addrcl 3bitr4 ) ABGHZCIJZUECKZLABCMHZIJZAUHKZ
          LUECNJAUHNJUFUIUGUJABCDEFOUGUHAKUJABCADPBEPCFPQUHARSTUECABDEUAFUBAUHD
          BCEFUCUBUD $.
          $([3-Aug-99]$)

        $( Inequality relation between subtraction and addition. $)
        ltaddsub $p |- ( ( A + B ) < C <-> A < ( C - B ) ) $=
          ( cneg caddc co clt wbr cmin recn negrcl cn0 negid opreq2i addid1
          eqtr2 addass1r breq1i subneg breq2i addrcl ltadd1 3bitr4r ) ACBGZHIZJ
          KABHIZUGHIZUHJKACBLIZJKUICJKAUJUHJABUGAADMZBEMZUGBENZMULABUGHIZHIAOHI
          AUOOAHBUMPQAULRSTUAUKUHAJCBCFMUMUBUCUICUGABDEUDFUNUEUF $.
          $([14-May-99]$)

        ${
          lt.4 $e |- D e. RR $.
          $( Adding both side of two inequalities.  Theoren I.25 of Apostol,
             p. 20. $)
          lt2add $p |- ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) $=
            ( caddc co clt wbr addrcl lttr ltadd1 ltadd2 syl2anb ) ABIJZCBIJZKL
            SCDIJZKLRTKLACKLBDKLRSTABEFMCBGFMCDGHMNACBEGFOBDCFHGPQ $.
            $([14-May-99]$)
        $}
      $}

      $( Addition of 2 positive numbers is positive. $)
      addgt0 $p |- ( ( 0 _10 < A /\ 0 _10 < B ) -> 0 _10 < ( A + B ) ) $=
        ( cn0 clt wbr caddc co ax0re addrcl lttr ltadd2 recn addid1 breq1i
        bitr sylan5b ) EAFGAABHIZFGZESFGEBFGZEASJCABCDKLUAAEHIZSFGTEBAJDCMUBASF
        AACNOPQR $.
        $([16-May-99]$)

      $( Addition of 2 non-negative numbers is non-negative. $)
      addge0 $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) -> 0 _10 <_ ( A + B ) ) $=
        ( cn0 clt wbr wceq wo caddc co cle wa addgt0 ax0re addrcl ltle syl
        opreq1 recn addid2 syl5eqr breq2d biimpa opreq2 addid1 biimpac opreq12
        0cn eqle ccase leloe syl2anb ) EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUNU
        PUOUQUSUNUPMEURFGZUSABCDNEUROABCDPZQZRUOUPMUTUSUOUPUTUOBUREFUOEBJKURBEA
        BJSBBDTUAUBUCUDVBRUNUQMUTUSUQUNUTUQAUREFUQAEJKURAEBAJUEAACTUFUBUCUGVBRU
        OUQMZEURHUSVCEEJKUREEAEBJUHEUIUFUBEUROVAUJRUKEAOCULEBODULUM $.
        $([28-May-99]$)

      ${
        addgt0i.3 $e |- 0 _10 < A $.
        addgt0i.4 $e |- 0 _10 < B $.
        $( Addition of 2 positive numbers is positive. $)
        addgt0i $p |- 0 _10 < ( A + B ) $=
          ( cn0 clt wbr caddc co addgt0 mp2an ) GAHIGBHIGABJKHIABCDLEFM $.
          $([18-May-99]$)
      $}

      $( Two non-negative numbers are zero iff their sum is zero. $)
      add20 $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
        ( ( A + B ) = 0 _10 <-> ( A = 0 _10 /\ B = 0 _10 ) ) ) $=
        ( cn0 cle wbr wa caddc co wceq clt wo wi wn addrcl ax0re lttri3 pm3.27
        sylbi addgt0 nsyl3 pm2.21d cleqcom biimp a1d opreq1 recn addid1
        addcoml syl5eqr cleq1d biimprd jcad opreq2 ccase2 leloe syl2anb 0cn
        syl6eq sylan9eq a1i impbid ) EAFGZEBFGZHZABIJZEKZAEKZBEKZHZEALGZEAKZMEB
        LGZEBKZMVHVKNZVDVEVLVNVMVOVPVLVNHZVHVKVHEVGLGZVQVHVGELGOZVROZHVTVGEABCD
        PQRVSVTSTABCDUAUBUCVMVHVIVJVMVIVHVMVIEAUDUEUFVMVJVHVMBVGEVMEBIJZVGBEABI
        UGBEBBDUHZEQUHWBBWBUIUJUKULUMUNVOVHVIVJVOVIVHVOAVGEVOAEIJVGAEBAIUOAACUH
        UIUKULUMVOVJVHVOVJEBUDUEUFUNUPEAQCUQEBQDUQURVKVHNVFVIVJVGWAEAEBIUGVJWAE
        EIJEBEEIUOEUSUIUTVAVBVC $.
        $([28-Jul-99]$)

      $( Negation of both sides of an inequality.  Theorem I.23 of Apostol,
         p. 20. $)
      ltneg $p |- ( A < B <-> -u B < -u A ) $=
        ( clt wbr cneg caddc co negrcl addrcl ltadd1 recn cn0 negid opreq1i
        addid2 eqtr addass2l add12 opreq2i addid1 3eqtr breq12i bitr ) ABEFAAGZ
        BGZHIZHIZBUHHIZEFUGUFEFABUHCDUFUGACJZBDJZKLUIUGUJUFEAUFUGUGACMZUFUKMZUG
        ULMZUOAUFHIZUGHINUGHIUGUPNUGHAUMOPUGUOQRSUJUFBUGHIZHIUFNHIUFBUFUGBDMZUN
        UOTUQNUFHBUROUAUFUNUBUCUDUE $.
        $([21-Jan-97]$)

      $( Negation of both sides of an inequality. $)
      leneg $p |- ( A <_ B <-> -u B <_ -u A ) $=
        ( clt wbr wceq wo cneg cle ltneg recn neg11 cleqcom bitr2 bi2or leloe
        negrcl 3bitr4 ) ABEFZABGZHBIZAIZEFZUBUCGZHABJFUBUCJFTUDUAUEABCDKUEBAGUA
        BABDLACLMBANOPABCDQUBUCBDRACRQS $.
        $([1-Aug-99]$)

      $( Contraposition of negation in an inequality. $)
      ltnegcon1 $p |- ( -u A < B <-> -u B < A ) $=
        ( cneg clt wbr negrcl ltneg recn negneg breq2i bitr ) AEZBFGBEZNEZFGOAF
        GNBACHDIPAOFAACJKLM $.
        $([14-May-99]$)

      $( Contraposition of negation in an inequality. $)
      ltnegcon2 $p |- ( A < -u B <-> B < -u A ) $=
        ( cneg clt wbr negrcl ltneg recn negneg breq1i bitr ) ABEZFGNEZAEZFGBPF
        GANCBDHIOBPFBBDJKLM $.
        $([14-May-99]$)

      $( The product of two positive numbers is positive. $)
      mulgt0 $p |- ( ( 0 _10 < A /\ 0 _10 < B ) -> 0 _10 < ( A x. B ) ) $=
        ( cr wcel cn0 clt wbr wa cmulc co wi axmulgt0 mp2an ) AEFBEFGAHIGBHIJGA
        BKLHIMABNCDO $.
        $([16-May-99]$)

      $( The product of two non-negative numbers is non-negative. $)
      mulge0 $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) -> 0 _10 <_ ( A x. B ) ) $=
        ( cn0 clt wbr wceq wo cmulc co cle wa mulgt0 ax0re mulrcl ltle syl
        opreq1 recn mulzer2 syl5eqr eqle opreq2 mulzer1 ccase2 leloe syl2anb )
        EAFGZEAHZIEBFGZEBHZIEABJKZLGZEALGEBLGUIUKUJULUNUIUKMEUMFGUNABCDNEUMOABC
        DPZQRUJEUMHZUNUJEBJKUMEEABJSBBDTUAUBEUMOUOUCZRULUPUNULAEJKUMEEBAJUDAACT
        UEUBUQRUFEAOCUGEBODUGUH $.
        $([30-Jul-99]$)

      ${
        mulgt0i.3 $e |- 0 _10 < A $.
        mulgt0i.4 $e |- 0 _10 < B $.
        $( The product of two positive numbers is positive. $)
        mulgt0i $p |- 0 _10 < ( A x. B ) $=
          ( cn0 clt wbr cmulc co mulgt0 mp2an ) GAHIGBHIGABJKHIABCDLEFM $.
          $([18-May-99]$)
      $}

      ${
        ltmullem.1 $e |- C e. RR $.
        $( Multiplication of both sides of an inequality by a positive
           number.  Theorem I.19 of Apostol, p. 20. $)
        ltmullem $p |- ( 0 _10 < C -> ( A < B -> ( A x. C ) < ( B x. C ) ) ) $=
          ( cn0 clt wbr cmin co cmulc subrcl mulgt0 exp comm caddc recn addid2
          breq1i ax0re ltaddsub bitr3 syl5ib mulrcl subdistr breq2i bitr4
          syl6ibr ) GCHIZABHIZGBAJKZCLKZHIZACLKZBCLKZHIZUJGULHIZUNUKURUJUNURUJU
          NULCBAEDMFNOPUKGAQKZBHIURUSABHAADRZSTGABUADEUBUCUDUQGUOQKZUPHIZUNVAUO
          UPHUOUOACDFUEZRSTVBGUPUOJKZHIUNGUOUPUAVCBCEFUEUBUMVDGHBACBERUTCFRUFUG
          UHUCUI $.
          $([15-May-99]$)
      $}
    $}

    $( L.t. is irreflexive. $)
    ltnr $p |- -. A < A $=
      ( cr wcel clt wbr wn ltnrt ax-mp ) ACDAAEFGBAHI $.
      $([18-Aug-99]$)

    $( L.e. is reflexive. $)
    leid $p |- A <_ A $=
      ( cr wcel cle wbr leidt ax-mp ) ACDAAEFBAGH $.
      $([18-Aug-99]$)

    $( A non-zero square is positive.  Theorem I.20 of Apostol, p. 20. $)
    sqgt0 $p |- ( -. A = 0 _10 -> 0 _10 < ( A x. A ) ) $=
      ( cn0 wceq wn clt wbr wo cmulc co ax0re lttri2 cneg ltneg neg0 breq1i
      bitr negrcl mulgt0 anabss recn mul2neg breq2i sylib sylbi jaoi ) ACDEACFG
      ZCAFGZHCAAIJZFGZACBKLUGUJUHUGCAMZFGZUJUGCMZUKFGULACBKNUMCUKFOPQULCUKUKIJZ
      FGZUJULUOUKUKABRZUPSTUNUICFAAABUAZUQUBUCUDUEUHUJAABBSTUFUE $.
      $([17-Jan-97]$)

    $( A square is non-negative. $)
    sqge0 $p |- 0 _10 <_ ( A x. A ) $=
      ( cn0 wceq cmulc co cle wbr clt wo opreq2 recn mulzer1 syl6req olc syl
      ax0re mulrcl leloe sylibr wn sqgt0 ltle pm2.61i ) ACDZCAAEFZGHZUECUFIHZCU
      FDZJZUGUEUIUJUEUFACEFCACAEKAABLMNUIUHOPCUFQAABBRZSTUEUAUHUGABUBCUFQUKUCPU
      D $.
      $([14-May-99]$)
  $}

  ${
    ltne0.1 $e |- A e. RR $.
    ltne0.2 $e |- 0 _10 < A $.
    $( L.t. implies not equal. $)
    ltne0 $p |- A =/= 0 _10 $=
      ( cn0 wne necom clt wbr ax0re ltne ax-mp mpbi ) DAEZADEDAFDAGHMCDAIBJKL
      $.
      $([15-May-99]$)
  $}

  $( Trichotomy law. $)
  letrit $p |- ( ( A e. RR /\ B e. RR ) -> ( A <_ B \/ B <_ A ) ) $=
    ( cr wcel cle wbr wo cn0 cded wceq breq1 breq2 bi2ord ax0re elimel letri
    dedth2h ) ACDZBCDZABEFZBAEFZGRAHIZBEFZBUBEFZGUBSBHIZEFZUEUBEFZGABHHAUBJTUCU
    AUDAUBBEKAUBBELMBUEJUCUFUDUGBUEUBELBUEUBEKMUBUEAHCNOBHCNOPQ $.
    $([18-Aug-99]$)

  $( Inequality relation between subtraction and addition. $)
  ltsubaddt $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                  ( ( A - B ) < C <-> A < ( C + B ) ) ) $=
    ( cr wcel cmin co clt wbr caddc wb cn0 cded wceq opreq1 breq1d breq1
    bi2bid opreq2 breq2d breq2 ax0re elimel ltsubadd dedth3h ) ADEZBDEZCDEZABFG
    ZCHIZACBJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMCUQJGZHIZKURUHC
    LMZHIZUMVBUQJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNURC
    HBUQUMFSPVGUKUTUMHBUQCJSTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJOTRUMUQVBALDU
    BUCBLDUBUCCLDUBUCUDUE $.
    $([21-Jan-97]$)

  $( Inequality relation between subtraction and addition. $)
  ltsubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) < C <-> A < ( B + C ) ) ) $=
    ( cr wcel w3a cmin co clt wbr caddc ltsubaddt wb wa wceq cc axaddcom recnt
    syl2an ancoms breq2d 3adant1 bitrd ) ADEZBDEZCDEZFABGHCIJACBKHZIJZABCKHZIJZ
    ABCLUEUFUHUJMUDUEUFNUGUIAIUFUEUGUIOZCPEBPEUKUFUECBQCRBRSTUAUBUC $.
    $([21-Jan-97]$)

  $( Inequality relation between subtraction and addition. $)
  lesubadd2t $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
                   ( ( A - B ) <_ C <-> A <_ ( B + C ) ) ) $=
    ( cr wcel cmin co cle wbr caddc wb cn0 cded wceq opreq1 breq1d breq1
    bi2bid opreq2 breq2d breq2 ax0re elimel lesubadd2 dedth3h ) ADEZBDEZCDEZABF
    GZCHIZABCJGZHIZKUFALMZBFGZCHIZUMUKHIZKABCLLLUMUGBLMZFGZCHIZUMUQCJGZHIZKURUH
    CLMZHIZUMUQVBJGZHIZKAUMNZUJUOULUPVFUIUNCHAUMBFOPAUMUKHQRBUQNZUOUSUPVAVGUNUR
    CHBUQUMFSPVGUKUTUMHBUQCJOTRCVBNZUSVCVAVECVBURHUAVHUTVDUMHCVBUQJSTRUMUQVBALD
    UBUCBLDUBUCCLDUBUCUDUE $.
    $([10-Aug-99]$)

  lt2addt $p |- ( ( ( A e. RR /\ B e. RR ) /\ ( C e. RR /\ D e. RR ) ) ->
                ( ( A < C /\ B < D ) -> ( A + B ) < ( C + D ) ) ) $=
    ( cr wcel clt wbr wa caddc co wi cn0 cded wceq breq1 birand opreq1 breq1d
    bi2imd biland opreq2 breq2 breq2d ax0re elimel lt2add dedth4h ) AEFZBEFZCEF
    ZDEFZACGHZBDGHZIZABJKZCDJKZGHZLABCDMMUIAMNZCGHZUNIZUSBJKZUQGHZLUTUJBMNZDGHZ
    IZUSVDJKZUQGHZLUSUKCMNZGHZVEIZVGVIDJKZGHZLVJVDULDMNZGHZIZVGVIVNJKZGHZLMMAUS
    OZUOVAURVCVSUMUTUNAUSCGPQVSUPVBUQGAUSBJRSTBVDOZVAVFVCVHVTUNVEUTBVDDGPUAVTVB
    VGUQGBVDUSJUBSTCVIOZVFVKVHVMWAUTVJVECVIUSGUCQWAUQVLVGGCVIDJRUDTDVNOZVKVPVMV
    RWBVEVOVJDVNVDGUCUAWBVLVQVGGDVNVIJUBUDTUSVDVIVNAMEUEUFBMEUEUFCMEUEUFDMEUEUF
    UGUH $.
    $([15-Aug-99]$)

  $( Comparison to zero.  Theorem I.23 of Apostol, p. 20. $)
  lt0negt $p |- ( A e. RR -> ( A < 0 _10 <-> 0 _10 < -u A ) ) $=
    ( cr wcel cn0 clt wbr cneg wb cded wceq breq1 negeq breq2d bi2bid neg0
    breq2i ax0re elimel ltnegcon2 bitr3 dedth ) ABCZADEFZDAGZEFZHUBADIZDEFZDUFG
    ZEFZHADAUFJZUCUGUEUIAUFDEKUJUDUHDEAUFLMNUGUFDGZEFUIUKDUFEOPUFDADBQRQSTUA $.
    $([14-May-99]$)

  $( A non-zero square is positive.  Theorem I.20 of Apostol, p. 20. $)
  ltsqt $p |- ( A e. RR -> ( -. A = 0 _10 -> 0 _10 < ( A x. A ) ) ) $=
    ( cr wcel cn0 wceq wn cmulc co clt wbr wi cded cleq1 binegd id opreq12d
    breq2d bi2imd ax0re elimel sqgt0 dedth ) ABCZADEZFZDAAGHZIJZKUCADLZDEZFZDUH
    UHGHZIJZKADAUHEZUEUJUGULUMUDUIAUHDMNUMUFUKDIUMAUHAUHGUMOZUNPQRUHADBSTUAUB
    $.
    $([6-May-99]$)

  $( 0 is less than 1.  Theorem I.21 of Apostol, p. 20. $)
  lt01 $p |- 0 _10 < 1 _10 $=
    ( cn0 cn1 cmulc co clt wceq wn wbr wne df-ne ax1ne0 mpbi ax1re sqgt0 ax-mp
    1cn mulid1 breqtr ) ABBCDZBEBAFGZASEHBAITBAJKLBMNOBPQR $.
    $([17-Jan-97]$)

  ${
    eqneg.1 $e |- A e. CC $.
    $( A number equal to its negative is zero. $)
    eqneg $p |- ( A = -u A <-> A = 0 _10 ) $=
      ( cneg wceq cn0 cn1 caddc co wn cmulc wo opreq2 1p1times negid cleqcomi
      3eqtr4g 1cn addcl mul0or syl ord wne df-ne ax1re addrcl lt01 addgt0i
      ltne0 mpbi mpi id negeq cleq12d cmin df-neg 0cn subid eqtr2 mpbiri impbi
      ) AACZDZAEDZVBFFGHZEDZIZVCVBVEVCVBVDAJHZEDVEVCKVBAAGHAVAGHZVGEAVAAGLABMVH
      EABNOPVDAFFQQRBSTUAVDEUBVFVDEUCVDFFUDUDUEFFUDUDUFUFUGUHUIUJVCVBEECZDVCAEV
      AVIVCUKAEULUMVIEEUNHEEUOEUPUQURUSUT $.
      $([29-May-99]$)

    $( A number is non-zero iff its negative is non-zero. $)
    negne0 $p |- ( A =/= 0 _10 <-> -u A =/= 0 _10 ) $=
      ( cn0 wceq wn cneg wne negneg cleq2i cleqcom bitr2 eqneg negcl 3bitr3
      bineg df-ne 3bitr4 ) ACDZEAFZCDZEACGSCGRTASDZSSFZDZRTUCSADUAUBASABHISAJKA
      BLSABMLNOACPSCPQ $.
      $([10-Aug-99]$)
  $}

  ${
    ltpl1.1 $e |- A e. RR $.
    $( A number is less than itself plus one. $)
    ltpl1 $p |- A < ( A + 1 _10 ) $=
      ( cn0 caddc co cn1 clt wbr recn addid1 breq1i ax0re ax1re ltadd2 lt01
      mpbi ) ACDEZAFDEZGHZARGHQARGAABIJKCFGHSCFALMBNOPP $.
      $([9-Aug-99]$)
  $}

  $( Hypothesis for deduction theorem to eliminate ` 0 _10 < A ` . $)
  elimgt0 $p |- 0 _10 < ded ( 0 _10 < A , A , 1 _10 ) $=
    ( cn0 clt wbr cn1 cded breq2 lt01 elimhyp ) BACDZBJAEFZCDBECDAEAKBCGEKBCGHI
    $.
    $([15-May-99]$)

  $( Hypothesis for deduction theorem to eliminate ` 0 _10 <_ A ` . $)
  elimge0 $p |- 0 _10 <_ ded ( 0 _10 <_ A , A , 0 _10 ) $=
    ( cn0 cle wbr cded breq2 wceq cleqid ax0re eqle ax-mp elimhyp ) BACDZBMABEZ
    CDBBCDZABANBCFBNBCFBBGOBHBBIIJKL $.
    $([30-Jul-99]$)

  ${
    recgt0.1 $e |- A e. RR $.
    ${
      recgt0i.2 $e |- 0 _10 < A $.
      $( The reciprocal of a positive number is positive.  Exercise 4 of
         Apostol, p. 21. $)
      recgt0i $p |- 0 _10 < ( 1 _10 / A ) $=
        ( cn0 cn1 cdiv co clt wbr wceq wo wn cr wcel wb axlttri ax0re ax1re
        ltne0 divrcl mp2an wa ioran wne df-ne necom 1cn recn ax1ne0 divneq0
        mpbi cneg cmulc negrcl mulgt0 mpan2 mulneg1 mulcom recid eqtr negeqi
        breq2i sylib lt0negt ax-mp 3imtr4 lt01 ltnsym mto pm3.2i mpbir ) DEAFGZ
        HIZDVLJZVLDHIZKLZDMNVLMNZVMVPODVLPQEARBABCSZTZUAVPVNLZVOLZUBVNVOUCVTWAD
        VLUDZVTDVLUEVLDUDWBVLDUFEAUGABUHZUIVRUJUKUKVOEDHIZDVLULZHIZDEULZHIZVOWD
        WFDWEAUMGZHIZWHWFDAHIWJWEAVLVSUNBUOCUPWIWGDHWIVLAUMGZULWGVLAVLVSUHZWCUQ
        WKEWKAVLUMGEVLAWLWCURAWCVRUSUTVAUTVBVCVQVOWFOVSVLVDVEEMNWDWHOREVDVEVFDE
        HIWDLVGDEQRVHVEVIVJVKVK $.
        $([15-May-99]$)
    $}

    $( The reciprocal of a positive number is positive.  Exercise 4 of Apostol,
       p. 21. $)
    recgt0 $p |- ( 0 _10 < A -> 0 _10 < ( 1 _10 / A ) ) $=
      ( cn0 clt wbr cn1 cdiv co cded wceq opreq2 breq2d cr ax1re keepel
      elimgt0 recgt0i dedth ) CADEZCFAGHZDECFSAFIZGHZDEAFAUAJTUBCDAUAFGKLUASAFM
      BNOAPQR $.
      $([15-May-99]$)

    ${
      prodgt0.2 $e |- B e. RR $.
      ${
        prodgt0i.3 $e |- 0 _10 < A $.
        $( If a number and its product are positive, so is the multiplier. $)
        prodgt0i $p |- ( 0 _10 < ( A x. B ) -> 0 _10 < B ) $=
          ( cn0 cmulc co clt wbr cn1 cdiv wi recgt0i ax0re mulrcl ax1re ltne0
          divrcl ltmullem ax-mp recn mulzer2 mul23 recid opreq1i mulid2 eqtr
          breq12i sylib ) FABGHZIJZFKALHZGHZUKUMGHZIJZFBIJFUMIJULUPMACENFUKUMOA
          BCDPKAQCACERZSZTUAUNFUOBIUMUMURUBZUCUOAUMGHZBGHZBABUMACUBZBDUBZUSUDVA
          KBGHBUTKBGAVBUQUEUFBVCUGUHUHUIUJ $.
          $([15-May-99]$)
      $}

      $( If a number and its product are positive, so is the multiplier. $)
      prodgt0 $p |- ( ( 0 _10 < A /\ 0 _10 < ( A x. B ) ) -> 0 _10 < B ) $=
        ( cn0 clt wbr cmulc co wi cn1 cded wceq opreq1 breq2d birimd cr ax1re
        keepel elimgt0 prodgt0i dedth imp ) EAFGZEABHIZFGZEBFGZUDUFUGJEUDAKLZBH
        IZFGZUGJAKAUHMZUFUJUGUKUEUIEFAUHBHNOPUHBUDAKQCRSDATUAUBUC $.
        $([15-May-99]$)

      ${
        divgt0lem.3 $e |- 0 _10 < B $.
        $( The ratio of two positive numbers is positive. $)
        divgt0lem $p |- ( 0 _10 < A -> 0 _10 < ( A / B ) ) $=
          ( cn0 clt wbr cn1 cdiv co cmulc ax1re ltne0 divrcl mulgt0 recgt0i
          mpan2 recn divrec breq2i sylibr ) FAGHZFAIBJKZLKZGHZFABJKZGHUCFUDGHUF
          AUDCIBMDBDENZOPBDEQRUGUEFGBABDSACSUHTUAUB $.
          $([16-May-99]$)
      $}
      $( The ratio of two positive numbers is positive. $)
      divgt0 $p |- ( ( 0 _10 < A /\ 0 _10 < B ) -> 0 _10 < ( A / B ) ) $=
        ( cn0 clt wbr cdiv co wi cn1 cded wceq opreq2 breq2d bilimd cr ax1re
        keepel elimgt0 divgt0lem dedth comm imp ) EAFGZEBFGZEABHIZFGZUFUEUHUFUE
        UHJUEEAUFBKLZHIZFGZJBKBUIMZUHUKUEULUGUJEFBUIAHNOPAUICUFBKQDRSBTUAUBUCUD
        $.
        $([16-May-99]$)

      ${
        divgt0i.3 $e |- 0 _10 < A $.
        divgt0i.4 $e |- 0 _10 < B $.
        $( The ratio of two positive numbers is positive. $)
        divgt0i $p |- 0 _10 < ( A / B ) $=
          ( cn0 clt wbr cdiv co divgt0lem ax-mp ) GAHIGABJKHIEABCDFLM $.
          $([18-May-99]$)
      $}

      $( The ratio of non-negative and positive numbers is non-negative. $)
      divge0 $p |- ( ( 0 _10 <_ A /\ 0 _10 < B ) -> 0 _10 <_ ( A / B ) ) $=
        ( cn0 cle wbr clt cdiv co wceq wo divgt0 exp comm opreq1 cleq1d wne
        ax0re ltne necom sylib cn1 cded opreq2 cc recn 1cn keepel elimne0
        divzer dedth syl syl5bi cleqcom syl6ib im2ord leloe syl5ib cr wcel wa
        wb divrclt jctil leloet sylibrd imp ) EAFGZEBHGZEABIJZFGZVJVIVLVJVIEVKH
        GZEVKKZLZVLVJEAHGZEAKZLVOVIVJVPVMVQVNVPVJVMVPVJVMABCDMNOVJVQVKEKZVNVQVJ
        VRVQEBIJZEKZVRVJVQVSVKEEABIPQVJBERZVTVJEBRWAEBSDTEBUAUBZWAVTEWABUCUDZIJ
        ZEKBUCBWCKVSWDEBWCEIUEQWCWABUCUFBDUGUHUIBUJUKULUMUNOVKEUOUPUQEASCURUSVJ
        EUTVAZVKUTVAZVBVLVOVCVJWFWEVJWAWFWBABCDVDUMSVEEVKVFUMVGOVH $.
        $([12-Aug-99]$)
    $}

    ${
      ltmul1.2 $e |- B e. RR $.
      ltmul1.3 $e |- C e. RR $.
      ${
        ltmul1i.4 $e |- 0 _10 < C $.
        $( Multiplication of both sides of an inequality by a positive
           number.  Theorem I.19 of Apostol, p. 20. $)
        ltmul1i $p |- ( A < B <-> ( A x. C ) < ( B x. C ) ) $=
          ( clt wbr cmulc co cn0 wi ltmullem ax-mp cn1 cdiv recgt0i mulrcl
          ax1re ltne0 divrcl recn divcl mulass recid opreq2i mulid1 3eqtr
          breq12i sylib impbi ) ABHIZACJKZBCJKZHIZLCHIUMUPMGABCDEFNOUPUNPCQKZJK
          ZUOUQJKZHIZUMLUQHIUPUTMCFGRUNUOUQACDFSBCEFSPCTFCFGUAZUBNOURAUSBHURACU
          QJKZJKAPJKAACUQADUCZCFUCZPCPTUCVDVAUDZUEVBPAJCVDVAUFZUGAVCUHUIUSBVBJK
          BPJKBBCUQBEUCZVDVEUEVBPBJVFUGBVGUHUIUJUKUL $.
          $([16-May-99]$)
      $}

      $( Multiplication of both sides of an inequality by a positive
         number.  Theorem I.19 of Apostol, p. 20. $)
      ltmul1 $p |- ( 0 _10 < C -> ( A < B <-> ( A x. C ) < ( B x. C ) ) ) $=
        ( cn0 clt wbr cmulc co wb cn1 cded wceq opreq2 breq12d bilbid cr ax1re
        keepel elimgt0 ltmul1i dedth ) GCHIZABHIZACJKZBCJKZHIZLUFAUECMNZJKZBUJJ
        KZHIZLCMCUJOZUIUMUFUNUGUKUHULHCUJAJPCUJBJPQRABUJDEUECMSFTUACUBUCUD $.
        $([16-May-99]$)

      $( Multiplication of both sides of an inequality by a positive
         number.  Theorem I.19 of Apostol, p. 20. $)
      ltmul2 $p |- ( 0 _10 < C -> ( A < B <-> ( C x. A ) < ( C x. B ) ) ) $=
        ( cn0 clt wbr cmulc co ltmul1 recn mulcom breq12i syl6bb ) GCHIABHIACJK
        ZBCJKZHICAJKZCBJKZHIABCDEFLQSRTHACADMCFMZNBCBEMUANOP $.
        $([16-May-99]$)

      $( Multiplication of both sides of an inequality by a positive
         number. $)
      lemul2 $p |- ( 0 _10 < C -> ( A <_ B <-> ( C x. A ) <_ ( C x. B ) ) ) $=
        ( cn0 clt wbr wceq wo cmulc co cle ltmul2 wi opreq2 a1i wne ax0re ltne
        necom cc wcel w3a mulcant2 recn 3pm3.2i mpan2 sylbi syl impbid bi2ord
        leloe mulrcl 3bitr4g ) GCHIZABHIZABJZKCALMZCBLMZHIZUTVAJZKABNIUTVANIUQU
        RVBUSVCABCDEFOUQUSVCUSVCPUQABCLQRUQGCSZVCUSPZGCTFUAVDCGSZVEGCUBVFCUCUDZ
        AUCUDZBUCUDZUEVECABUFVGVHVICFUGADUGBEUGUHUIUJUKULUMABDEUNUTVACAFDUOCBFE
        UOUNUP $.
        $([1-Aug-99]$)

      $( Multiplication of both sides of an inequality by a positive
         number. $)
      lemul1 $p |- ( 0 _10 < C -> ( A <_ B <-> ( A x. C ) <_ ( B x. C ) ) ) $=
        ( cn0 clt wbr cle cmulc co lemul2 recn mulcom breq12i syl6bb ) GCHIABJI
        CAKLZCBKLZJIACKLZBCKLZJIABCDEFMRTSUAJCACFNZADNOCBUBBENOPQ $.
        $([2-Aug-99]$)

      ${
        ltdivi.4 $e |- 0 _10 < C $.
        $( Division of both sides of an inequality by a positive number. $)
        ltdivi $p |- ( A < B <-> ( A / C ) < ( B / C ) ) $=
          ( clt wbr cn1 cdiv co cmulc ax1re ltne0 divrcl recgt0i ltmul1i recn
          divrec breq12i bitr4 ) ABHIAJCKLZMLZBUCMLZHIACKLZBCKLZHIABUCDEJCNFCFG
          OZPCFGQRUFUDUGUEHCACFSZADSUHTCBUIBESUHTUAUB $.
          $([16-May-99]$)
      $}

      $( Division of both sides of an inequality by a positive number. $)
      ltdiv $p |- ( 0 _10 < C -> ( A < B <-> ( A / C ) < ( B / C ) ) ) $=
        ( cn0 clt wbr cdiv co wb cn1 cded wceq opreq2 breq12d bilbid cr ax1re
        keepel elimgt0 ltdivi dedth ) GCHIZABHIZACJKZBCJKZHIZLUFAUECMNZJKZBUJJK
        ZHIZLCMCUJOZUIUMUFUNUGUKUHULHCUJAJPCUJBJPQRABUJDEUECMSFTUACUBUCUD $.
        $([16-May-99]$)
    $}
  $}

  ${
    lt2sq.1 $e |- A e. RR $.
    lt2sq.2 $e |- B e. RR $.
    $( Two non-negative numbers compare the same as their squares. $)
    lt2sq $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
                ( A < B <-> ( A x. A ) < ( B x. B ) ) ) $=
      ( cn0 clt wbr wceq wo cmulc co wb cle wa ltmul2 ltmul1 bi2anan9 anabs
      syl5bb mulrcl lttr syl6bi wn lemul2 lemul1 bi2anan9r letr lelt 3imtr3g
      a3d impbid breq1 adantr ax0re opreq2 breq1d recn mulzer1 eqtr4 breq1i
      sylan9bbr bitr3d adantl sylan9bb 3bitr3g bicon4d bicomd ltneq cleqid mt2
      mtbii jca pm5.21 syl ccase leloe syl2anb ) EAFGZEAHZIEBFGZEBHZIABFGZAAJKZ
      BBJKZFGZLZEAMGZEBMGVRVTVSWAWFVRVTNZWBWEWHWBWCABJKZFGZWIWDFGZNZWEWHWBWBNWL
      WBVRWBWJVTWBWKABACDCOABBCDDPQWBRSWCWIWDAACCTZABCDTBBDDTZUAUBWHWBWEWHBAMGZ
      WDWCMGZWBUCZWEUCZWHWOWDBAJKZMGZWSWCMGZNZWPWHWOWONXBWOVTWOWTVRWOXABABDCDUD
      BAADCCUEUFWORSWDWSWCWNBADCTWMUGUBBADCUHZWDWCWNWMUHZUIUJUKVSVTNVTWBWEVSVTW
      BLVTEABFULZUMVTVTBEJKZWDFGZVSWEEBBUNDDOVSAEJKZWDFGWEXGVSXHWCWDFEAAJUOUPXF
      XHWDFXFEXHBBDUQURZAACUQURZUSUTSZVAVBVRWANZWBWEXLWOWPWQWRXLWGWOWPWAWGWOLVR
      EBAMULVCVRWGXHWCMGZWAWPEAAUNCCUDWAXFWCMGWPXMWAXFWDWCMEBBJUOZUPXHXFWCMXHEX
      FXJXIUSUTSVDVBXCXDVEVFVSWANZWQWRNWFXOWQWRXOBBFGZWBWAXPVTVSWBWAVTXPEBBFULV
      GXEVAXPBBHBBDDVHBVIVJVKXOWDWDFGZWEWAXQXGVSWEWAXGXQWAXFWDWDFXNUPVGXKVAXQWD
      WDHWDWDWNWNVHWDVIVJVKVLWBWEVMVNVOEAUNCVPEBUNDVPVQ $.
      $([3-Aug-99]$)

    $( Two non-negative numbers compare the same as their squares. $)
    le2sq $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
                ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) ) $=
      ( cn0 cle wbr cmulc co wb wa clt wn lt2sq binegd lelt mulrcl 3bitr4g
      ancoms ) EBFGZEAFGZABFGZAAHIZBBHIZFGZJTUAKZBALGZMUDUCLGZMUBUEUFUGUHBADCNO
      ABCDPUCUDAACCQBBDDQPRS $.
      $([2-Aug-99]$)

    $( The square of a non-negative number is a one-to-one function. $)
    sq11 $p |- ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
                ( ( A x. A ) = ( B x. B ) <-> A = B ) ) $=
      ( cn0 cle wbr wa wceq cmulc co clt wo wn lt2sq wb ancoms bi2ord lttri2
      mulrcl 3bitr4g bicon4d bicomd ) EAFGZEBFGZHZABIZAAJKZBBJKZIZUFUGUJUFABLGZ
      BALGZMUHUILGZUIUHLGZMUGNUJNUFUKUMULUNABCDOUEUDULUNPBADCOQRABCDSUHUIAACCTB
      BDDTSUAUBUC $.
      $([29-Jul-99]$)
  $}

  $( Two non-negative numbers compare the same as their squares. $)
  le2sqt $p |- ( ( A e. RR /\ B e. RR ) -> ( ( 0 _10 <_ A /\ 0 _10 <_ B ) ->
              ( A <_ B <-> ( A x. A ) <_ ( B x. B ) ) ) ) $=
    ( cr wcel cn0 cle wbr wa cmulc co wb wi cded wceq breq2 birand breq1
    opreq12 anabss breq1d bi2bid bi2imd biland breq2d ax0re elimel le2sq
    dedth2h ) ACDZBCDZEAFGZEBFGZHZABFGZAAIJZBBIJZFGZKZLEUIAEMZFGZULHZUSBFGZUSUS
    IJZUPFGZKZLUTEUJBEMZFGZHZUSVFFGZVCVFVFIJZFGZKZLABEEAUSNZUMVAURVEVMUKUTULAUS
    EFOPVMUNVBUQVDAUSBFQVMUOVCUPFVMUOVCNAUSAUSIRSTUAUBBVFNZVAVHVEVLVNULVGUTBVFE
    FOUCVNVBVIVDVKBVFUSFOVNUPVJVCFVNUPVJNBVFBVFIRSUDUAUBUSVFAECUEUFBECUEUFUGUH
    $.
    $([3-Aug-99]$)

  ${
    halfpos.1 $e |- A e. RR $.
    $( A positive number is greater than its half. $)
    halfpos $p |- ( 0 _10 < A <-> ( A / ( 1 _10 + 1 _10 ) ) < A ) $=
      ( cn0 clt wbr caddc co cn1 cdiv cmulc ax0re ltadd1 recn addid2 1p1times
      cleqcomi breq12i ax1re addrcl mulrcl lt01 addgt0i ltdivi bitr 1cn addcl
      ltne0 divcan3 breq2i 3bitr ) CADECAFGZAAFGZDEZAHHFGZIGZUNAJGZUNIGZDEZUOAD
      ECAAKBBLUMAUPDEURUKAULUPDAABMZNUPULAUSOPQAUPUNBUNAHHRRSZBTUTHHRRUAUAUBZUC
      UDUQAUODUNAHHUEUEUFUSUNUTVAUGUHUIUJ $.
      $([18-May-99]$)
  $}

  ${
    $d x A $.  $d x B $.
    posex.1 $e |- A e. RR $.
    posex.2 $e |- B e. RR $.
    posex.3 $e |- 0 _10 < A $.
    posex.4 $e |- 0 _10 < B $.
    $( There exists a positive number less than two others. $)
    posex $p |- E. x e. RR ( 0 _10 < x /\ ( x < A /\ x < B ) ) $=
      ( clt wbr wceq wo cn0 cv wa cr wrex or1 wn lttri2 biimp orri mpbi cn1
      caddc co cdiv wcel ax1re addrcl lt01 addgt0i ltne0 divrcl lttr halfpos
      mpan jctil divgt0i breq2 breq1 bi2and rcla4ev syl mpbiri jaoi jctir
      ax-mp ) BCHIZBCJZCBHIZKZKZLAMZHIZVMBHIZVMCHIZNZNZAOPZVIVHVJKZKVLVIVHVJQVI
      VTVIRVTBCDESTUAUBVHVSVKVHBUCUCUDUEZUFUEZOUGZLWBHIZWBBHIZWBCHIZNZNZNVSVHWH
      WCVHWGWDVHWFWEWEVHWFWBBCBWADUCUCUHUHUIZWAWIUCUCUHUHUJUJUKZULZUMZDEUNLBHIW
      EBDUOFUBZUPWMUQBWADWIFWJURUQWLUQVRWHAWBOVMWBJZVNWDVQWGVMWBLHUSWNVOWEVPWFV
      MWBBHUTVMWBCHUTVAVAVBVCVKCWAUFUEZOUGZLWOHIZWOBHIZWOCHIZNZNZNVSVKXAWPVKWTW
      QVKWRWSVIWRVJVIWRWSBCWOHUSLCHIWSCEUOGUBZVDWSVJWRWOCBCWAEWIWKUMZEDUNXBUPVE
      XBVFCWAEWIGWJURUQXCUQVRXAAWOOVMWOJZVNWQVQWTVMWOLHUSXDVOWRVPWSVMWOBHUTVMWO
      CHUTVAVAVBVCVEVG $.
      $([18-May-99]$)
  $}

