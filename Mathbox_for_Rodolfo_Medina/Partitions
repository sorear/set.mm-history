$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                                  Partitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    prtlem60.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    prtlem60.2 $e |- ( ps -> ( th -> ta ) ) $.
    $( Lemma for ~ prter3 . $)
    prtlem60 $p |- ( ph -> ( ps -> ( ch -> ta ) ) ) $=
      ( wi a1i syldd ) ABCDEFBDEHHAGIJ $.
      $( [9-Oct-2010] $)
  $}

  ${
    impbidd.1 $e |- ( ph -> ( ps -> ( ch -> th ) ) ) $.
    impbidd.2 $e |- ( ph -> ( ps -> ( th -> ch ) ) ) $.
    $( Lemma for ~ prter3 . $)
    impbidd $p |- ( ph -> ( ps -> ( ch <-> th ) ) ) $=
      ( wb wa wi imp impbid ex ) ABCDGABHCDABCDIEJABDCIFJKL $.
      $( [12-Oct-2010] $)
  $}

  ${
    bicomdd.1 $e |- ( ph -> ( ps -> ( ch <-> th ) ) ) $.
    $( Commute two sides of a biconditional in a deduction.  (The proof was
       shortened by Andrew Salmon, 29-Jun-2011.) $)
    bicomdd $p |- ( ph -> ( ps -> ( th <-> ch ) ) ) $=
      ( wb bicom syl6ib ) ABCDFDCFECDGH $.
      $( [29-Jun-2011] $) $( [19-Oct-2010] $)

    $( Commute two sides of a biconditional in a deduction. $)
    bicomddOLD $p |- ( ph -> ( ps -> ( th <-> ch ) ) ) $=
      ( wb wa imp bicomd ex ) ABDCFABGCDABCDFEHIJ $.
      $( [19-Oct-2010] $)
  $}

  ${
    prtlem1.1 $e |- ( ps -> ( ch -> ph ) ) $.
    $( Add a disjunct in the antecedent. $)
    prtlem1 $p |- ( ( ph \/ ps ) -> ( ch -> ph ) ) $=
      ( wi ax-1 jaoi ) ACAEBACFDG $.
      $( [24-Sep-2010] $)
  $}

  ${
    jca2.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca2.2 $e |- ( ps -> th ) $.
    $( Inference conjoining the consequents of two implications. $)
    jca2 $p |- ( ph -> ( ps -> ( ch /\ th ) ) ) $=
      ( wi a1i jcad ) ABCDEBDGAFHI $.
      $( [12-Oct-2010] $)
  $}

  ${
    jca2r.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca2r.2 $e |- ( ps -> th ) $.
    $( Inference conjoining the consequents of two implications. $)
    jca2r $p |- ( ph -> ( ps -> ( th /\ ch ) ) ) $=
      ( wi a1i jcad ) ABDCBDGAFHEI $.
      $( [17-Oct-2010] $)
  $}

  ${
    jca3.1 $e |- ( ph -> ( ps -> ch ) ) $.
    jca3.2 $e |- ( th -> ta ) $.
    $( Inference conjoining the consequents of two implications. $)
    jca3 $p |- ( ph -> ( ps -> ( th -> ( ch /\ ta ) ) ) ) $=
      ( wa wi imp a1d jca2 ex ) ABDCEHIABHZDCENCDABCFJKGLM $.
      $( [14-Oct-2010] $)
  $}

  ${
    prtlem50.1 $e |- ( ph -> ( ps -> ch ) ) $.
    prtlem50.2 $e |- ( th -> ta ) $.
    $( Lemma for ~ prter3 . $)
    prtlem50 $p |- ( ph -> ( ( ps /\ th ) -> ( ch /\ ta ) ) ) $=
      ( wi a1i anim12d ) ABCDEFDEHAGIJ $.
      $( [12-Oct-2010] $)
  $}

  $( Rearrangement of 4 conjuncts.  (The proof was shortened by Andrew Salmon,
     29-Jun-2011.) $)
  an43 $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
               ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) $=
    ( wa an42 bicomi ) ADEBCEEABECDEEADBCFG $.
    $( [29-Jun-2011] $) $( [24-Sep-2010] $)

  $( Rearrangement of 4 conjuncts. $)
  an43OLD $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) <->
               ( ( ph /\ th ) /\ ( ps /\ ch ) ) ) $=
    ( wa ancom anbi2i an4 bitri ) ABEZCDEZEJDCEZEADEBCEEKLJCDFGABDCHI $.
    $( [24-Sep-2010] $)

  $( A rearrangement of conjuncts. $)
  an3 $p |- ( ( ( ph /\ ps ) /\ ( ch /\ th ) ) -> ( ph /\ th ) ) $=
    ( wa an43 simplbi ) ABECDEEADEBCEABCDFG $.
    $( [25-Sep-2010] $)

  $( Lemma for ~ prter3 : a rearrangement of conjuncts. $)
  prtlem70 $p |- ( ( ( ( ps /\ et ) /\ ( ( ph /\ th ) /\ ( ch /\ ta ) ) )
                        /\ ph ) <-> ( ( ph /\ ( ps /\ ( ch /\ ( th /\ ta ) ) )
    ) /\ et ) ) $=
    ( wa anass anbi1i anandi ancom bitr4i 3bitr2i 3bitri 3bitr4ri an4 bitri
    anbi2i ) BFGZADGZCEGZGZGZAGZABDGZGZUAGZFGZAUEUAGZGZFGABCDEGZGGZGZFGABGZTGZU
    AGZFGUNUBGZFGZUHUDUPUQFUNTUAHIUGUPFUFUOUAABDJIIUDFUNGZUBGZFUQGURUDASGZUBGZU
    NFGZUBGUTUDAUCGVBUCAKASUBHLVCVAUBABFHIVCUSUBUNFKIMFUNUBHFUQKNOUGUJFAUEUAHIU
    JUMFUIULAUIBCGUKGULBDCEPBCUKHQRIN $.
    $( [20-Oct-2010] $)

  ${
    ibdr.1 $e |- ( ph -> ( ch -> ( ps <-> ch ) ) ) $.
    $( Reverse of ibd. $)
    ibdr $p |- ( ph -> ( ch -> ps ) ) $=
      ( bicomdd ibd ) ACBACBCDEF $.
      $( [30-Sep-2010] $)
  $}

  $( Variant of ~ pm5.31 . $)
  pm5.31r $p |- ( ( ch /\ ( ph -> ps ) ) -> ( ph -> ( ch /\ ps ) ) ) $=
    ( wi wa pm3.2 imim2d imp ) CABDACBEZDCBIACBFGH $.
    $( [15-Oct-2010] $)

  $( Cancel a conjunct from the scope of an existential quantifier.  (The proof
     was shortened by Andrew Salmon, 29-Jun-2011.) $)
  exan3 $p |- ( E. x ( ph /\ ps ) -> E. x ps ) $=
    ( wa simpr eximi ) ABDBCABEF $.
    $( [29-Jun-2011] $) $( [25-Sep-2010] $)

  $( Cancel a conjunct from the scope of an existential quantifier. $)
  exan3OLD $p |- ( E. x ( ph /\ ps ) -> E. x ps ) $=
    ( wa wex 19.40 simprd ) ABDCEACEBCEABCFG $.
    $( [25-Sep-2010] $)

  $( Double the quantifiers of theorem r19.29. $)
  2r19.29 $p |- ( ( A. x e. A A. y e. B ph /\ E. x e. A E. y e. B ps )
                        -> E. x e. A E. y e. B ( ph /\ ps ) ) $=
    ( wral wrex wa r19.29 reximi syl ) ADFGZCEGBDFHZCEHIMNIZCEHABIDFHZCEHMNCEJO
    PCEABDFJKL $.
    $( [25-Sep-2010] $)

  $( Lemma for ~ prter3 . $)
  prtlem100 $p |- ( E. x e. A ( B e. x /\ ph ) <->
                        E. x e. ( A \ { (/) } ) ( B e. x /\ ph ) ) $=
    ( cv wcel wa c0 csn cdif wne anass eldifsn anbi1i ne0i pm4.71ri bitri
    anbi2i 3bitr4ri rexbii2 ) DBEZFZAGZUCBCCHIJZUACFZUAHKZGZUCGUEUFUCGZGUAUDFZU
    CGUEUCGUEUFUCLUIUGUCUACHMNUCUHUEUCUFUBGZAGUHUBUJAUBUFUADOPNUFUBALQRST $.
    $( [19-Oct-2010] $)

  ${
    $d u v x r $.  $d u v x s $.  $d u v x A $.
    $( Lemma for ~ prter1 , ~ prter2 , ~ prter3 and ~ prtex . $)
    prtlem5 $p |- ( [ s / v ] [ r / u ] E. x e. A ( u e. x /\ v e. x )
                        <-> E. x e. A ( r e. x /\ s e. x ) ) $=
      ( cv wcel wa wrex wsbc wex df-rex sbbii sbex bitri sban exbii 3bitri
      ax-17 sbf elsb3 anbi12i anbi2i anbi1i bitr4i ) CGAGZHZBGUGHZIZADJZCFGZKZB
      EGZKZUGDHZULUGHZUNUGHZIZIZALZUSADJUOUPBUNKZUQUIIZBUNKZIZALZVBUQBUNKZUIBUN
      KZIZIZALZVAUOUPCULKZUJCULKZIZBUNKZALZUPVCIZBUNKZALVFUOUPUJIZCULKZALZBUNKZ
      VTBUNKZALVPUOVSALZCULKZBUNKWBUMWEBEUKWDCFUJADMNNWEWABEVSACFONPVTABEOWCVOA
      VTVNBEUPUJCFQNRSVOVRAVNVQBEVLUPVMVCUPCFUPCTUAVMUHCULKZUICULKZIVCUHUICFQWF
      UQWGUIFCAUBUICFUICTUAUCPUCNRVRVEAUPVCBEQRSVEVJAVDVIVBUQUIBEQUDRVKVBUSIZAL
      VAVJWHAVIUSVBVGUQVHURUQBEUQBTUAEBAUBUCUDRWHUTAVBUPUSUPBEUPBTUAUERPSUSADMU
      F $.
      $( [25-Sep-2010] $)
  $}

  $( Lemma for ~ prter2 . $)
  prtlem90 $p |- ( -. A e. B -> ( C e. B -> C =/= A ) ) $=
    ( wcel wn wne wceq wa wb wo olc ancom orbi2i sylib xor sylibr eleq1 nsyl
    ex df-ne syl6ibr necom syl6ib ) ABDZEZCBDZACFZCAFUEUFACGZEZUGUEUFUIUEUFHZUD
    UFIZUHUJUDUFEHZUFUEHZJZUKEUJULUJJUNUJULKUJUMULUEUFLMNUDUFOPACBQRSACTUAACUBU
    C $.
    $( [17-Oct-2010] $)

  $( Lemma for ~ prter2 . $)
  prtlem80 $p |- ( A e. B -> -. A e. ( C \ { A } ) ) $=
    ( wcel csn wn wa cdif wi snidg a1d iman sylib eldif notbii sylibr ) ABDZACD
    ZAAEZDZFGZFZACSHDZFQRTIUBQTRABJKRTLMUCUAACSNOP $.
    $( [17-Oct-2010] $)

  ${
    $d x A $.  $d x u $.
    $( Negated membership of the empty set in another class. $)
    n0el $p |- ( -. (/) e. A <-> A. x e. A E. u u e. x ) $=
      ( cv wcel wn wal wral wi wex c0 df-ral df-ex ralbii wa alnex imnan albii
      wrex 0el df-rex bitri notbii 3bitr4ri ) BDADZEZFBGZFZACHUECEZUHIZAGZUFBJZ
      ACHKCEZFZUHACLULUHACUFBMNUIUGOZFZAGUOAJZFUKUNUOAPUJUPAUIUGQRUMUQUMUGACSUQ
      ABCTUGACUAUBUCUDUD $.
      $( [25-Sep-2010] $)
  $}

  ${
    $d x A $.
    ceqsex3.1OLD $e |- ( ps -> A. x ps ) $.
    ceqsex3.2OLD $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Version of ~ ceqsex with an antecedent instead of a hypothesis.  (Use
       ~ ceqsexg instead of this one. --NM 13-Aug-11) $)
    ceqsex3OLD $p |- ( A e. _V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( cvv wcel cv wceq wa wex biimpa 19.23ai wi wal biimprcd 19.21ai exintr
      syl isset syl5ib com12 impbid2 ) DGHZCIDJZAKZCLZBUGBCEUFABFMNBUEUHBUFCLZU
      HUEBUFAOZCPUIUHOBUJCEUFABFQRUFACSTCDUAUBUCUD $.
      $( [19-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x ps $.
    ceqsex3v.1OLD $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Version of ~ ceqsexv with an antecedent instead of a hypothesis.  (Use
       ~ ceqsexgv instead of this one. --NM 13-Aug-11) $)
    ceqsex3vOLD $p |- ( A e. _V -> ( E. x ( x = A /\ ph ) <-> ps ) ) $=
      ( cvv ceqsexgv ) ABCDFEG $.
      $( [19-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdv.1 $e |- Rel A $.
    eqrelrdv.2 $e |- Rel B $.
    eqrelrdv.3 $e |- ( ph -> ( <. x , y >. e. A <-> <. x , y >. e. B ) ) $.
    $( Deduce equality of relations from equivalence of membership. $)
    eqrelrdv $p |- ( ph -> A = B ) $=
      ( cv cop wcel wb wal wceq 19.21aivv wrel eqrel mp2an sylibr ) ABICIJZDKTE
      KLZCMBMZDENZAUABCHODPEPUCUBLFGBCDEQRS $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqbrrdv.1 $e |- Rel A $.
    eqbrrdv.2 $e |- Rel B $.
    eqbrrdv.3 $e |- ( ph -> ( x A y <-> x B y ) ) $.
    $( Deduction from extensionality principle for relations. $)
    eqbrrdv $p |- ( ph -> A = B ) $=
      ( cv wbr cop wcel df-br 3bitr3g eqrelrdv ) ABCDEFGABIZCIZDJPQEJPQKZDLRELH
      PQDMPQEMNO $.
      $( [10-Oct-2010] $)
  $}

  ${
    $d x y z $.  $d x y w $.
    $( Closed form of ~ brabsb . $)
    brabsb2 $p |- ( R = { <. x , y >. | ph }
                        -> ( z R w <-> [ w / y ] [ z / x ] ph ) ) $=
      ( copab wceq cv wbr cop wcel wsbc breq df-br syl6bb opelopabsb ) FABCGZHZ
      DIZEIZFJZTUAKRLZABTMCUAMSUBTUARJUCTUAFRNTUAROPABCDEQP $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqrelrdv2.1 $e |- ( ( ( Rel A /\ Rel B ) /\ ph )
                                -> ( <. x , y >. e. A
                                        <-> <. x , y >. e. B ) ) $.
    $( Other version of ~ eqrelrdv . $)
    eqrelrdv2 $p |- ( ( ( Rel A /\ Rel B ) /\ ph ) -> A = B ) $=
      ( wrel wa wceq cv cop wcel wb wal 19.21aivv eqrel adantr mpbird ) DGEGHZA
      HZDEIZBJCJKZDLUBELMZCNBNZTUCBCFOSUAUDMABCDEPQR $.
      $( [30-Sep-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d ph x $.  $d ph y $.
    eqbrrdv2.1 $e |- ( ( ( Rel A /\ Rel B ) /\ ph )
                        -> ( x A y <-> x B y ) ) $.
    $( Other version of ~ eqbrrdv . $)
    eqbrrdv2 $p |- ( ( ( Rel A /\ Rel B ) /\ ph ) -> A = B ) $=
      ( wrel wa cv wbr cop wcel df-br 3bitr3g eqrelrdv2 ) ABCDEDGEGHAHBIZCIZDJP
      QEJPQKZDLRELFPQDMPQEMNO $.
      $( [30-Sep-2010] $)
  $}

  ${
    $d x y z R $.  $d x y A $.  $d x y B $.
    ersym2.1 $e |- A e. _V $.
    ersym2.2 $e |- B e. _V $.
    $( Version of ~ ersym with an antecedent instead of a hypothesis. $)
    ersym2 $p |- ( Er R -> ( A R B -> B R A ) ) $=
      ( vx vy vz wer cv wbr wi wceq wa breq12 wb ancoms imbi12d imbi2d wal
      dfer2 biimpi 19.21bi 19.21bbi simpld vtocl2 ) CIZFJZGJZCKZUIUHCKZLZLUGABC
      KZBACKZLZLFGABDEUHAMZUIBMZNZULUOUGURUJUMUKUNUHAUIBCOUQUPUKUNPUIBUHACOQRSU
      GULUJUIHJZCKNUHUSCKLZUGULUTNZGHUGVAHTGTZFUGVBFTFGHCUAUBUCUDUEUF $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    ertr2.1 $e |- A e. _V $.
    ertr2.2 $e |- B e. _V $.
    ertr2.3 $e |- C e. _V $.
    $( Version of ~ ertr with an antecedent instead of a hypothesis. $)
    ertr2 $p |- ( Er R -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( vx vy vz wer cv wbr wa wi wceq breq1 anbi1d imbi12d imbi2d breq2
      anbi12d imbi1d anbi2d syl3an9b wal dfer2 biimpi 19.21bi 19.21bbi simprd
      vtocl3 ) DKZHLZILZDMZUOJLZDMZNZUNUQDMZOZOZUMABDMZBCDMZNZACDMZOZOZHIJABCEF
      GUNAPZVBUMAUODMZURNZAUQDMZOZOUOBPZUMVCBUQDMZNZVLOZOUQCPZVHVIVAVMUMVIUSVKU
      TVLVIUPVJURUNAUODQRUNAUQDQSTVNVMVQUMVNVKVPVLVNVJVCURVOUOBADUAUOBUQDQUBUCT
      VRVQVGUMVRVPVEVLVFVRVOVDVCUQCBDUAUDUQCADUASTUEUMUPUOUNDMOZVAUMVSVANZIJUMV
      TJUFIUFZHUMWAHUFHIJDUGUHUIUJUKUL $.
      $( [10-Oct-2010] $)
  $}

  ${
    ersymb2.1 $e |- A e. _V $.
    ersymb2.2 $e |- B e. _V $.
    $( Version of ~ ersymb with an antecedent instead of a hypothesis. $)
    ersymb2 $p |- ( Er R -> ( A R B <-> B R A ) ) $=
      ( wer wbr ersym2 impbid ) CFABCGBACGABCDEHBACEDHI $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y $.  $d y R $.  $d x A $.  $d x R $.
    $( Closed form of ~ erref . $)
    erreft $p |- ( Er R -> ( A e. ( dom R u. ran R ) -> A R A ) ) $=
      ( vx vy wer cv wbr cdm crn cun wral wcel wi wo wex wa visset ertr2
      ersym2 sylan2d anidm syl5ibr 19.23adv eldm syl5ib syland elrn jaod elun
      r19.21aiv wceq breq1 breq2 bitrd rcla4cv syl ) BEZCFZURBGZCBHZBIZJZKAVBLA
      ABGZMUQUSCVBUQURUTLZURVALZNUSURVBLUQVDUSVEUQURDFZBGZDOUSVDUQVGUSDUQVGVGPU
      SVGUQVGVFURBGZUSVGURVFURBCQZDQZVIRZURVFBVIVJSTVGUAUBUCDURBVIUDUEUQVHDOUSV
      EUQVHUSDUQVHVHPUSVHUQVGVHUSVHVKVFURBVJVISUFVHUAUBUCDURBVIUGUEUHURUTVAUIUE
      UJUSVCCAVBURAUKUSAURBGVCURAURBULURAABUMUNUOUP $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y R $.
    $( Closed form of ~ erdmrn . $)
    erdmrn2 $p |- ( Er R -> dom R = ran R ) $=
      ( vx vy wer cdm crn cv wbr wex wcel visset ersymb2 exbidv eldm elrn
      3bitr4g eqrdv ) ADZBAEZAFZRBGZCGZAHZCIUBUAAHZCIUASJUATJRUCUDCUAUBABKZCKLM
      CUAAUENCUAAUEOPQ $.
      $( [11-Oct-2010] $)
  $}

  $( Simplified version of ~ erreft . $)
  erreft2 $p |- ( Er R -> ( A e. dom R -> A R A ) ) $=
    ( wer cdm wcel crn cun wbr erdmrn2 uneq2d unidm syl5reqr eleq2d erreft
    sylbird ) BCZABDZEAQBFZGZEAABHPSQAPQQGSQPQRQBIJQKLMABNO $.
    $( [11-Oct-2010] $)

  $( An equivalence relation is a set if its domain is a set. $)
  erex $p |- ( ( Rel R /\ Er R ) -> ( dom R e. A -> R e. _V ) ) $=
    ( wrel wer wa cdm wcel crn cxp cvv wi pm5.31r wb erdmrn2 eleq1d biimpd
    pm4.71 sylib xpexg syl6bi sylan2 wss ssexg relssdmrn sylan syl6 ) BCZBDZEBF
    ZAGZUGUIBHZIZJGZEZBJGZUGUJUMKUJUNKUHUJUMUGLUHUJUJUKAGZEZUMUHUJUPKUJUQMUHUJU
    PUHUIUKABNOPUJUPQRUIUKAASTUABULUBUMUOUGBULJUCBUDUEUF $.
    $( [15-Oct-2010] $)

  $( An equivalence relation is a set if and only if its domain is a set. $)
  erex2 $p |- ( ( Rel R /\ Er R ) -> ( R e. _V <-> dom R e. _V ) ) $=
    ( wrel wer wa cvv wcel cdm dmexg erex impbid2 ) ABACDAEFAGEFAEHEAIJ $.
    $( [15-Oct-2010] $)

  ${
    $d x A $.  $d x B $.
    $( Lemma for ~ prter3 . $)
    prtlem9 $p |- ( A e. B -> E. x e. B [ x ] R = [ A ] R ) $=
      ( wcel cv wceq wrex cec risset eceq2 reximi sylbi ) BCEAFZBGZACHNDIBDIGZA
      CHABCJOPACNBDKLM $.
      $( [25-Sep-2010] $)
  $}

  ${
    $d v z $.  $d v w $.  $d v R $.
    $( Lemma for ~ prter3 . $)
    prtlem10 $p |- ( Er R -> ( z e. dom R ->
                                ( z R w <->
                                        E. v e. dom R ( z e. [ v ] R /\
                                                        w e. [ v ] R ) ) ) ) $=
      ( wer cv cdm wcel wbr cec wa wrex erreft2 visset elec syl6ibr biimpri
      jca3 wi wceq prtlem9 eleq2 anbi12d biimprd reximi syl r19.37av prtlem60
      ersym2 syl5ib biimpi prtlem50 ertr2 syld reximdv c0 wne wb ne0i r19.9rzv
      syl9 impbidd ) DEZAFZDGZHZVDBFZDIZVDCFZDJZHZVGVJHZKZCVELZVCVFVHVDVDDJZHZV
      GVOHZKZVNVCVFVPVHVQVCVFVDVDDIVPVDDMVDVDDANZVSOPVQVHVGVDDBNZVSOQRVFVRVMSZC
      VELZVRVNSVFVJVOTZCVELWBCVDVEDUAWCWACVEWCVMVRWCVKVPVLVQVJVOVDUBVJVOVGUBUCU
      DUEUFVRVMCVEUGUFUHVCVNVHCVELZVFVHVCVMVHCVEVCVMVDVIDIZVIVGDIZKVHVCVKWEVLWF
      VCVIVDDIWEVKVIVDDCNZVSUIVDVIDVSWGOUJVLWFVGVIDVTWGOUKULVDVIVGDVSWGVTUMUNUO
      VFVHWDVFVEUPUQVHWDURVEVDUSVHCVEUTUFUDVAVB $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y R $.
    $( Members of a quotient set do not overlap. $)
    erdisj3 $p |- ( Er R -> ( ( B e. ( A /. R ) /\ C e. ( A /. R ) )
                                -> ( B = C \/ ( B i^i C ) = (/) ) ) ) $=
      ( vy vx wer cqs wcel wceq cin c0 wo cv cec wex wi wa eqeq1 ineq1 eqeq1d
      orbi12d eqeq2 ineq2 sylan9bb visset erdisj2 syl5cbi exp3a 19.23adv imp
      ex wrex elqsi df-rex sylib exan3 syl eqcom exbii syl5 syl7 imp3a ) DGZBAD
      HZIZCVEIZBCJZBCKZLJZMZVDVFENZDOZCJZEPZVKVGVDFNZDOZBJZFPZVOVKQZVFVDVSVTVDV
      SRVNVKEVDVSVNVKQZVDVRWAFVDVRVNVKVRVNRVQVMJZVQVMKZLJZMZVKVDVRWEBVMJZBVMKZL
      JZMVNVKVRWBWFWDWHVQBVMSVRWCWGLVQBVMTUAUBVNWFVHWHVJVMCBUCVNWGVILVMCBUDUAUB
      UEVPVLDFUFEUFUGUHUIUJUKUJULVFBVQJZFPZVSVFVPAIZWIRFPZWJVFWIFAUMWLFABDUNWIF
      AUOUPWKWIFUQURWIVRFBVQUSUTUPVAVGCVMJZEPZVOVGVLAIZWMREPZWNVGWMEAUMWPEACDUN
      WMEAUOUPWOWMEUQURWMVNECVMUSUTUPVBVC $.
      $( [12-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( Closed form of ~ elqs . $)
    elqs2 $p |- ( B e. C -> ( B e. ( A /. R ) <-> E. x e. A B = [ x ] R ) ) $=
      ( vy cv cec wceq wrex cqs eqeq1 rexbidv df-qs elab2g ) FGZAGEHZIZABJCQIZA
      BJFCBEKDPCIRSABPCQLMAFBENO $.
      $( [12-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x R $.
    $( Lemma for ~ prter2 . $)
    prtlem11 $p |- ( B e. D ->
                        ( C e. A -> ( B = [ C ] R -> B e. ( A /. R ) ) ) ) $=
      ( vx wcel cec wceq cqs cv wrex wa elqs2 r19.41v eqtr3 eqcomd eceq2 sylan
      reximi sylbir risset sylanb syl5bir exp3a ) BDGZCAGZBCEHZIZBAEJGZUFUJBFKZ
      EHZIZFALZUGUIMFABDENUKCIZFALZUIUNUGUPUIMUOUIMZFALUNUOUIFAOUQUMFAULUHIZUIU
      MUOURUIMULBULBUHPQUKCERSTUAFCAUBUCUDUE $.
      $( [12-Oct-2010] $)
  $}

  ${
    $d x R $.  $d x A $.
    $( Closed form of ~ 0nelqs . $)
    0nelqs2 $p |- ( dom R = A -> -. (/) e. ( A /. R ) ) $=
      ( vx cdm wceq c0 cv cec wrex cqs wcel wn wral eleq2 wne visset ecdmn0
      necom df-ne 3bitri syl5bbr biimprd r19.21aiv ralnex sylib elqsi nsyl ) BD
      ZAEZFCGZBHZEZCAIZFABJKUIULLZCAMUMLUIUNCAUIUNUJAKZUIUJUHKZUOUNUHAUJNUPUKFO
      FUKOUNUJBCPQUKFRFUKSTUAUBUCULCAUDUECAFBUFUG $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y $.
    $( Lemma for ~ prtex and ~ prter3 . $)
    prtlem12 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> Rel R ) $=
      ( cv wcel wa wrex copab wceq wrel relopab releq mpbiri ) EAFCFZGBFPGHCDIZ
      ABJZKELRLQABMERNO $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z $.  $d u v w $.
    $d u v A $.
    $( Lemma for ~ prter1 , ~ prter2 , ~ prter3 and ~ prtex . $)
    prtlem13 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> ( z R w <-> E. v e. A ( z e. v /\ w e. v ) ) ) $=
      ( cv wcel wa wrex copab wceq wbr wsbc brabsb2 prtlem5 syl6bb elequ2
      anbi12d cbvrexv ) HAIFIZJBIUCJKFGLZABMNZCIZDIZHOZUFUCJZUGUCJZKZFGLZUFEIZJ
      ZUGUMJZKZEGLUEUHUDAUFPBUGPULUDABCDHQFBAGDCRSUKUPFEGUCUMNUIUNUJUOFECTFEDTU
      AUBS $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u w z A $.  $d u v w z A $.  $d w z R $.
    $( Lemma for ~ prtex , ~ prter2 and ~ prter3 . $)
    prtlem16 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> dom R = U. A ) $=
      ( vz vv vw cv wcel wa wrex copab wceq cdm cuni wex wbr prtlem13 exbidv
      visset eldm syl5bb df-rex anass exbii bitr4i syl6bb excom exdistr
      exsimpl syl6bi exancom syl6ib eluni syl6ibr df-clel exan3 sylbi adantl
      ancli eximi syl5bir impbid eqrdv ) EAICIZJBIVFJKCDLABMNZFEOZDPZVGFIZVHJZV
      JVIJZVGVKVJGIZJZVMDJZKGQZVLVGVKVOVNKZGQZVPVGVKVQHIZVMJZHQZKZGQZVRVGVKVQVT
      KZHQGQZWCVGVKWDGQZHQZWEVGVKVNVTKZGDLZHQZWGVGVJVSERZHQWJVKVGWKWIHABFHGCDES
      THVJEFUAUBUCWIWFHWIVOWHKZGQWFWHGDUDWDWLGVOVNVTUEUFUGUFUHWDHGUIUHVQVTGHUJU
      HZVQWAGUKULVOVNGUMZUNGVJDUOZUPVGVKWCVLWMVLVRWCVLVPVRWOWNUGVQWBGVQWAVNWAVO
      VNVSVJNZVTKHQWAHVJVMUQWPVTHURUSUTVAVBUSVCVDVE $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( Lemma for ~ prter2 and also a property of partitions . $)
    prtlem400 $p |- ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) }
                        -> -. (/) e. ( U. A /. R ) ) $=
      ( cv wcel wa wrex copab wceq cdm cuni c0 cqs wn prtlem16 0nelqs2 syl ) EA
      FCFZGBFTGHCDIABJKELDMZKNUAEOGPABCDEQUAERS $.
      $( [15-Oct-2010] $)
  $}

  $( Introduce the partition predicate. $)
  $c Prt $.

  $( Extend the definition of a wff to include the partition predicate. $)
  wprt $a wff Prt A $.

  ${
    $d x y A $.
    $( Define the partition predicate. $)
    df-prt $a |- ( Prt A <-> A. x e. A A. y e. A ( x = y \/
                                                ( x i^i y ) = (/) ) ) $.
  $}

  ${
    $d x y A $.  $d x y R $.
    $( The quotient set of an equivalence relation is a partition. $)
    erprt $p |- ( Er R -> Prt ( A /. R ) ) $=
      ( vx vy wer cv wceq cin c0 wo cqs wral wprt erdisj3 r19.21aivv df-prt
      sylibr ) BEZCFZDFZGSTHIGJZDABKZLCUBLUBMRUACDUBUBASTBNOCDUBPQ $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d w x $.  $d w y $.
    $( Lemma for ~ prter1 , ~ prter2 and ~ prtex . $)
    prtlem14 $p |- ( Prt A -> ( ( x e. A /\ y e. A )
                                -> ( ( w e. x /\ w e. y ) -> x = y ) ) ) $=
      ( wprt cv wcel wa wceq cin c0 wo wi wral df-prt ra42 sylbi wn wal eq0
      ax-4 pm2.21d elin syl5ibr prtlem1 syl6 ) DEZAFZDGBFZDGHZUHUIIZUHUIJZKIZLZ
      CFZUHGUOUIGHZUKMUGUNBDNADNUJUNMABDOUNABDDPQUKUMUPUMUOULGZUKUPUMUQUKUMUQRZ
      CSURCULTURCUAQUBUOUHUIUCUDUEUF $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d z x $.  $d z y $.  $d y w $.
    $( Lemma for ~ prter2 . $)
    prtlem17 $p |- ( Prt A -> ( ( x e. A /\ z e. x )
                                        -> ( E. y e. A ( z e. y /\ w e. y )
                                                -> w e. x ) ) ) $=
      ( wprt cv wcel wa wrex wi wex wceq prtlem14 elequ2 biimprd syl8 exp4a
      imp3a an23 syl5ibr exp3a imp5a imp4b 19.23adv df-rex syl5ib ex ) EFZAGZEH
      ZCGZUJHZIZULBGZHZDGZUOHZIZBEJZUQUJHZKUIUNIZUOEHZUSIZBLVAUTVBVDVABUIUNVCUS
      VAUIUNVCUPURVAUIUNVCUPURVAKZKZUIUKVCIZUMIVFUNVCIUIVGUMVFUIVGUMUPVEUIVGUMU
      PIUJUOMZVEABCENVHVAURABDOPQRSUKVCUMTUAUBUCUDUEUSBEUFUGUH $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z $.  $d u v w $.
    $d u p z $.  $d u p w $.  $d u v p A $.  $d v R $.
    $( Lemma for ~ prter2 . $)
    prtlem18 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( ( v e. A /\ z e. v ) -> ( w e. v <-> z R w ) ) )
        $=
      ( vp cv wcel wa wrex copab wceq wprt wbr wb wi wex wal prtlem13 df-rex
      syl6bb biimprd 19.23v sylibr 19.21bi anass syl5ib exp3a a1d imp32 biimpd
      prtlem17 imp sylan9 impbid expr ancoms ) HAJFJZKBJVAKLFGMABNOZGPZEJZGKZCJ
      ZVDKZLZDJZVDKZVFVIHQZRZSVBVCVHVLVBVCVHLZLVJVKVBVCVHVJVKSZVBVHVNSVCVBVHVJV
      KVBVEVGVJLZLZVKVHVJLVBVPVKSZEVBVPETZVKSVQEUAVBVKVRVBVKVOEGMVRABCDEFGHUBVO
      EGUCUDUEVPVKEUFUGUHVEVGVJUIUJUKULUMVBVKVFIJZKVIVSKLIGMZVMVJVBVKVTABCDIFGH
      UBUNVCVHVTVJSEICDGUOUPUQURUSUT $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u w z A $.  $d u v w z A $.  $d v w R $.
    $( Lemma for ~ prter2 . $)
    prtlem19 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( ( v e. A /\ z e. v ) -> v = [ z ] R ) ) $=
      ( vw wprt cv wcel wa wrex copab wceq cec wbr wb prtlem18 bicomdd imp
      visset elec syl5bb eqrdv eqcomd ex ) FIGAJEJZKBJUHKLEFMABNOLZDJZFKCJZUJKL
      ZUJUKGPZOUIULLZUMUJUNHUMUJUNUKHJZGQZUOUJKZUOUMKUIULUPUQRUIULUQUPABCHDEFGS
      TUAUOUKGHUBCUBUCUDUEUFUG $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x z A $.  $d x y u $.  $d x y v $.  $d x z u $.
    $d x z v $.  $d w x $.  $d w y $.
    $( Lemma for ~ prter1 and ~ prtex . $)
    prtlem15 $p |- ( Prt A -> ( E. x e. A E. y e. A ( ( u e. x /\ w e. x )
                        /\ ( w e. y /\ v e. y ) )
                        -> E. z e. A ( u e. z /\ v e. z ) ) ) $=
      ( wprt cv wcel wa wrex wi wceq prtlem14 elequ2 anbi2d an3 syl5bir syl8
      imp4a anabs7 an43 anbi2i 3bitr4ri syl7ib expdimp r19.23adv reximdva
      anbi12d cbvrexv syl6ib ) GHZFIZAIZJZDIZUOJZKUQBIZJZEIZUSJZKKZBGLZAGLUPVAU
      OJZKZAGLUNCIZJZVAVGJZKZCGLUMVDVFAGUMUOGJZKVCVFBGUMVKUSGJZVCVFMZUMVKVLKZUR
      UTKZVCKZVFVCUMVNVOVCVFUMVNVOUOUSNZVMABDGOVQVFUPVBKZVCVQVEVBUPABEPQUPURUTV
      BRSTUAVOVRVOKZKVSVPVCVRVOUBVCVSVOUPURUTVBUCZUDVTUEUFUGUHUIVFVJACGUOVGNUPV
      HVEVIACFPACEPUJUKUL $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u p A $.  $d u v r A $.
    $d u v w q A $.  $d u v z p q A $.  $d z w p R $.  $d u r z $.  $d u r p $.
    $( Every partition generates an equivalence relation. $)
    prter1 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> Er R ) $=
      ( vz vw vp vv vr vq cv wcel wa wrex copab wceq wprt wer wbr wi wal
      prtlem13 ancom rexbii syl6bb bitr4d biimpd adantr anbi12d reeanv syl6bbr
      prtlem15 sylan9 wb sylibrd jca 19.21aivv 19.21aiv dfer2 sylibr ancoms )
      EALCLZMBLVCMNCDOABPQZDRZESZVDVENZFLZGLZETZVIVHETZUAZVJVIHLZETZNZVHVMETZUA
      ZNZHUBGUBZFUBVFVGVSFVGVRGHVGVLVQVDVLVEVDVJVKVDVJVIILZMZVHVTMZNZIDOZVKVDVJ
      WBWANZIDOZWDABFGICDEUCZWEWCIDWBWAUDUEUFABGFICDEUCUGUHUIVGVOVHJLZMVMWHMNJD
      OZVPVDVOWEVIKLZMVMWJMNZNKDOIDOZVEWIVDVOWLVDVOWFWKKDOZNWLVDVJWFVNWMWGABGHK
      CDEUCUJWEWKIKDDUKULUHIKJGHFDUMUNVDVPWIUOVEABFHJCDEUCUIUPUQURUSFGHEUTVAVB
      $.
      $( [13-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( Lemma for ~ prtex . $)
    prtlem300 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( R e. _V <-> U. A e. _V ) ) $=
      ( wprt cv wcel wa wrex copab wceq cvv cdm cuni wrel wer wb prtlem12
      adantl prter1 erex2 syl11anc prtlem16 eleq1d bitrd ) DFZEAGCGZHBGUHHICDJA
      BKLZIZEMHZENZMHZDOZMHZUJEPZEQUKUMRUIUPUGABCDESTABCDEUAEUBUCUIUMUORUGUIULU
      NMABCDEUDUETUF $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u A $.
    $( The equivalence relation generated by a partition is a set if and only
       if the partition itself is a set. $)
    prtex $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( R e. _V <-> A e. _V ) ) $=
      ( wprt cv wcel wa wrex copab wceq cvv cuni prtlem300 uniexb syl6bbr ) DFE
      AGCGZHBGRHICDJABKLIEMHDNMHDMHABCDEODPQ $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y u v z p A $.  $d v z p R $.
    $( The quotient set of the equivalence relation generated by a partition
       equals the partition itself. $)
    prter2 $p |- ( ( Prt A /\
                        R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } )
                        -> ( U. A /. R ) = ( A \ { (/) } ) ) $=
      ( vp vv vz wprt cv wcel wa wrex copab wceq cuni cqs c0 csn cdif wne cec
      wral wi prtlem19 r19.21aivv 2r19.29 ex syl wex rexcom4 r19.41v exbii
      bitri df-rex rexbii visset elqs eluni2 anbi1i 3bitri 3bitr4ri syl5ib
      eqtr3 reximi syl6 19.41v simprbi risset syl6ibr wn prtlem400 prtlem90
      adantl jcad eldifsn ancomsd elunii jca2r cvv prtlem11 ax-mp imp eximdv
      19.9v 3imtr3g 0ex prtlem80 n0el mpbi rspec eldifi jca syl5 impbid eqrdv
      ) DIZEAJCJZKBJWRKLCDMABNOZLZFDPZEQZDRSZTZWTFJZXBKZXEXDKZWTXFXEDKZXERUAZLX
      GWTXFXHXIWTXFGJZXEOZGDMZXHWTXFXKHXJMZGDMZXLWTXFXJHJZEUBZOZXEXPOZLZHXJMZGD
      MZXNWTXRHXJMZGDMZYAXFWTXQHXJUCGDUCZYCYAUDWTXQGHDXJABHGCDEUEUFYDYCYAXQXRGH
      DXJUGUHUIXOXJKZXRLZHUJZGDMZYEGDMZXRLZHUJZYCXFYHYFGDMZHUJYKYFGHDUKYLYJHYEX
      RGDULUMUNYBYGGDXRHXJUOUPXFXRHXAMXOXAKZXRLZHUJYKHXAXEEFUQZURXRHXAUOYNYJHYM
      YIXRGXODUSUTUMVAVBVCXTXMGDXSXKHXJXJXEXPVDVEVEVFXMXKGDXMYEHUJZXKXMYEXKLHUJ
      YPXKLXKHXJUOYEXKHVGUNVHVEVFGXEDVIVJWSXFXIUDZWQWSRXBKVKYQABCDEVLRXBXEVMUIV
      NVOXEDRVPVJWTXOXEKZHUJZXHLZXFXGWTYRXHLZHUJXFHUJYTXFWTUUAXFHWTUUAYNXFWTUUA
      XRYMWTXHYRXRABHFCDEUEVQXOXEDVRVSYMXRXFXEVTKYMXRXFUDUDYOXAXEXOVTEWAWBWCVFW
      DYRXHHVGXFHWEWFXGYSXHYSFXDRXDKVKZYSFXDUCRVTKUUBWGRVTDWHWBFHXDWIWJWKXEDXCW
      LWMWNWOWP $.
      $( [17-Oct-2010] $)
  $}

  ${
    $d x y u z $.  $d x y u w $.  $d x y u A $.  $d u v z A $.  $d u v w A $.
    $d u v S $.  $d z w R $.  $d z w t S $.  $d t v $.  $d t A $.
    $( For every partition there exists a unique equivalence relation whose
       quotient set equals the partition. $)
    prter3 $p |- ( ( R = { <. x , y >. | E. u e. A ( x e. u /\ y e. u ) } /\
                        ( Rel S /\
                                ( Er S /\
                                        ( S e. _V /\
                                                ( dom R = dom S /\
                                                        ( U. A /. S ) =
                                                        ( A \ { (/) } )
                        ) ) ) ) ) -> R = S ) $=
      ( vz vw vv vt cv wcel wa wrex copab wceq wrel wer cvv cdm cuni cqs c0
      csn cdif an12 anbi2i anabs5 bitri wbr wb eleq2 ad2antrl ad2antll
      prtlem70 cec prtlem10 imp eqeq1 prtlem16 syl5bi rexeq syl6com wex ecexg
      anbi12d ceqsexgv syl rexbidv rexcom4 syl5bbr df-rex exbii syl5rbbr
      visset elqs anbi1i 19.41v anass 3bitr2ri syl6bb syl6bbr sylan9bb
      prtlem100 prtlem13 bicomd sylbir ex bicomdd sylbid breldm syl5 ibd ibdr
      impbid adantl eqbrrdv2 anasss prtlem12 sylan ) EAKCKZLBKXALMCDNABOPZFQZFR
      ZFSLZETZFTZPZDUAZFUBZDUCUDUEZPZMMZMZMZMZXBXCXBXNMZMZMZEFPZXSXBXPMXPXRXPXB
      XCXBXNUFUGXBXOUHUIEQZXRXTXBYAXCXQXTXQGHEFXQGKZHKZEUJZYBYCFUJZUKZYAXCMXQYD
      YEXQYDYEXQYBXFLZYFYDXQYGYBXGLZYFXMYGYHUKZXBXDXHYIXEXLXFXGYBULUMUNXQYHYEYD
      XQYHYEYDUKZXQYHMXDYHMZXBXHMZXEXLMZMZMZXBMYJXBXDXEXHXLYHUOYOYEYBIKZLZYCYPL
      ZMZIDNZXBYDYKYEYBJKZFUPZLZYCUUBLZMZJXGNZYNYTXDYHYEUUFUKGHJFUQURYLUUFUUEJX
      INZYMYTXBXHUUFUUGUKZXHXBXGXIPZUUHXHXFXIPUUIXBXFXGXIUSABCDEUTVAUUEJXGXIVBV
      CURYMUUGYSIXKNZYTXEUUGYSIXJNZXLUUJXEUUGYPXJLZYSMZIVDZUUKXEUUGUUAXILZYPUUB
      PZYSMZMZJVDZIVDZUUNXEUUQJXINZIVDZUUGUUTXEUUQIVDZJXINUUGUVBXEUVCUUEJXIXEUU
      BSLUVCUUEUKUUASFVEYSUUEIUUBSUUPYQUUCYRUUDYPUUBYBULYPUUBYCULVFVGVHVIUUQJIX
      IVJVKUVAUUSIUUQJXIVLVMVNUUSUUMIUUMUUOUUPMZJVDZYSMUVDYSMZJVDUUSUULUVEYSUUL
      UUPJXINUVEJXIYPFIVOVPUUPJXIVLUIVQUVDYSJVRUVFUURJUUOUUPYSVSVMVTVMWAYSIXJVL
      WBYSIXJXKVBWCYRIDYBWDWBWCWCXBYDYTABGHICDEWEWFWCWGWHWIZWJYBYCEGVOZWKWLWMXQ
      YDYEXQYHYFYEUVGYBYCFUVHWKWLWNWOWPWQWRABCDEWSWTWG $.
      $( [19-Oct-2010] $)
  $}

$( (End of Rodolfo Medina's mathbox.) $)

