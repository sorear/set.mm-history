$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Principia Mathematica * 10
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    19.20ian.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( This theorem was not actually included in Principia Mathematica.
       However, it is included here to shorten the following proofs. $)
    19.20ian $p |- ( ( A. x ph /\ A. x ps ) -> A. x ch ) $=
      ( wal ex 19.20ii imp ) ADFBDFCDFABCDABCEGHI $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d ph x $.
    $( Theorem *10.12 in [WhiteheadRussell] p. 146.  In *10, this is treated
       like an axiom, and the proofs in *10 are based on this theorem. $)
    pm10.12 $p |- ( A. x ( ph \/ ps ) -> ( ph \/ A. x ps ) ) $=
      ( wo wal ax-17 19.32 biimpi ) ABDCEABCEDABCACFGH $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)
  $}

    $( Theorem *10.14 in [WhiteheadRussell] p. 146. $)
    pm10.14 $p |- ( ( A. x ph /\ A. x ps ) -> ( [ y / x ] ph /\ [ y / x ] ps )
    ) $=
      ( wal cv wsbc stdpc4 anim12i ) ACEACDFZGBCEBCJGACDHBCDHI $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.251 in [WhiteheadRussell] p. 149. $)
    pm10.251 $p |- ( A. x -. ph -> -. A. x ph ) $=
      ( wn wal wex alnex 19.2 con3i sylbi ) ACBDABEZCABDZCABFKJABBGHI $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.252 in [WhiteheadRussell] p. 149. $)
    pm10.252 $p |- ( -. E. x ph <-> A. x -. ph ) $=
      ( wn wal wex df-ex bicomi con1bii ) ACBDZABEZJICABFGH $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.253 in [WhiteheadRussell] p. 149. $)
    pm10.253 $p |- ( -. A. x ph <-> E. x -. ph ) $=
      ( wn wex wal alex bicomi con1bii ) ACBDZABEZJICABFGH $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.26 in [WhiteheadRussell] p. 149. $)
    pm10.26 $p |- ( ( A. x ( ph -> ps ) /\ [ y / x ] ph ) -> [ y / x ] ps ) $=
      ( wi wal cv wsbc a4sbim imp ) ABECFACDGZHBCKHABCDIJ $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.29 in [WhiteheadRussell] p. 150. $)
    pm10.29 $p |- ( ( A. x ( ph -> ps ) /\ A. x ( ph -> ch ) ) ->
        A. x ( ph -> ( ps /\ ch ) ) ) $=
      ( wi wa pm3.43 19.20ian ) ABEACEABCFEDABCGH $.
      $( [8-Jun-2011] $) $( [8-Jun-2011] $)

    $( Theorem *10.3 in [WhiteheadRussell] p. 150. $)
    pm10.3 $p |- ( ( A. x ( ph -> ps ) /\ A. x ( ps -> ch ) ) ->
        A. x ( ph -> ch ) ) $=
      ( wi pm3.33 19.20ian ) ABEBCEACEDABCFG $.
      $( [8-Jun-2011] $) $( [8-Jun-2011] $)

    $( Theorem *10.301 in [WhiteheadRussell] p. 151. $)
    pm10.301 $p |- ( ( A. x ( ph <-> ps ) /\ A. x ( ps <-> ch ) ) ->
        A. x ( ph <-> ch ) ) $=
      ( wb bitr 19.20ian ) ABEBCEACEDABCFG $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.31 in [WhiteheadRussell] p. 151. $)
    pm10.31 $p |- ( A. x ( ph -> ps ) ->
        A. x ( ( ph /\ ch ) -> ( ps /\ ch ) ) ) $=
      ( wi wa pm3.45 19.20i ) ABEACFBCFEDABCGH $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.311 in [WhiteheadRussell] p. 151. $)
    pm10.311 $p |- ( A. x ( ph <-> ps ) ->
        A. x ( ( ph /\ ch ) <-> ( ps /\ ch ) ) ) $=
      ( wb wa anbi1 19.20i ) ABEACFBCFEDABCGH $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.32 in [WhiteheadRussell] p. 151. $)
    pm10.32 $p |- ( A. x ( ph <-> ps ) <-> A. x ( ps <-> ph ) ) $=
      ( wb bicom albii ) ABDBADCABEF $.
      $( [27-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.321 in [WhiteheadRussell] p. 151. $)
    pm10.321 $p |- ( ( A. x ( ph <-> ps ) /\ A. x ( ph <-> ch ) ) ->
                   A. x ( ps <-> ch ) ) $=
      ( wb bibi1 biimpa 19.20ian ) ABEZACEZBCEZDIJKABCFGH $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.322 in [WhiteheadRussell] p. 151. $)
    pm10.322 $p |- ( ( A. x ( ps <-> ph ) /\ A. x ( ch <-> ph ) ) ->
        A. x ( ps <-> ch ) ) $=
      ( wb biantr 19.20ian ) BAECAEBCEDBACFG $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.39 in [WhiteheadRussell] p. 153. $)
    pm10.39 $p |- ( ( A. x ( ph -> ch ) /\ A. x ( ps -> th ) ) ->
        A. x ( ( ph /\ ps ) -> ( ch /\ th ) ) ) $=
      ( wi wa prth 19.20ian ) ACFBDFABGCDGFEACBDHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.4 in [WhiteheadRussell] p. 153. $)
    pm10.4 $p |- ( ( A. x ( ph <-> ch ) /\ A. x ( ps <-> th ) ) ->
        A. x ( ( ph /\ ps ) <-> ( ch /\ th ) ) ) $=
      ( wb wa pm4.38 19.20ian ) ACFBDFABGCDGFEABCDHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.411 in [WhiteheadRussell] p. 154. $)
    pm10.411 $p |- ( ( A. x ( ph <-> ch ) /\ A. x ( ps <-> th ) ) ->
        A. x ( ( ph \/ ps ) <-> ( ch \/ th ) ) ) $=
      ( wb wo pm4.39 19.20ian ) ACFBDFABGCDGFEABCDHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.412 in [WhiteheadRussell] p. 154. $)
    pm10.412 $p |- ( A. x ( ph <-> ps ) <-> A. x ( -. ph <-> -. ps ) ) $=
      ( wb wn notbi albii ) ABDAEBEDCABFG $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.413 in [WhiteheadRussell] p. 154. $)
    pm10.413 $p |- ( ( A. x ( ph <-> ch ) /\ A. x ( ps <-> th ) ) ->
        A. x ( ( ph -> ps ) <-> ( ch -> th ) ) ) $=
      ( wb wi wa pm3.26 pm3.27 imbi12d 19.20ian ) ACFZBDFZABGCDGFEMNHACBDMNIMNJ
      KL $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.414 in [WhiteheadRussell] p. 154. $)
    pm10.414 $p |- ( ( A. x ( ph <-> ch ) /\ A. x ( ps <-> th ) ) ->
        A. x ( ( ph <-> ps ) <-> ( ch <-> th ) ) ) $=
      ( wb wa pm3.26 pm3.27 bibi12d 19.20ian ) ACFZBDFZABFCDFFELMGACBDLMHLMIJK
      $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.42 in [WhiteheadRussell] p. 155. $)
    pm10.42 $p |- ( ( E. x ph \/ E. x ps ) <-> E. x ( ph \/ ps ) ) $=
      ( wo wex 19.43 bicomi ) ABDCEACEBCEDABCFG $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *10.43 in [WhiteheadRussell] p. 155. $)
    pm10.43 $p |- ( ( A. x ( ph <-> ps ) /\ [ y / x ] ph ) <->
        ( A. x ( ph <-> ps ) /\ [ y / x ] ps ) ) $=
      ( wb wal wsb a4sbbi pm5.32i ) ABECFACDGBCDGABCDHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.43 in [WhiteheadRussell] p. 155. $)
    pm10.51 $p |- ( -. E. x ( ph /\ ps ) <-> A. x ( ph -> -. ps ) ) $=
      ( wn wi wal wa wex alinexa bicomi ) ABDECFABGCHDABCIJ $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

  ${
    $d ps x $.
    $( Theorem *10.52 in [WhiteheadRussell] p. 155. $)
    pm10.52 $p |- ( E. x ph -> ( A. x ( ph -> ps ) <-> ps ) ) $=
      ( wex wi wal pm5.5 19.23v syl5bb ) ACDZJBEBABECFJBGABCHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

    $( Theorem *10.53 in [WhiteheadRussell] p. 155. $)
    pm10.53 $p |- ( -. E. x ph -> A. x ( ph -> ps ) ) $=
      ( wex wn wal wi pm2.21 19.38 syl ) ACDZEKBCFZGABGCFKLHABCIJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ch x $.
    $( Theorem *10.541 in [WhiteheadRussell] p. 155. $)
    pm10.541 $p |- ( A. x ( ph -> ( ch \/ ps ) ) <->
        ( ch \/ A. x ( ph -> ps ) ) ) $=
      ( wn wi wal wo bi2.04 albii 19.21v bitri df-or imbi2i 3bitr4i ) ACEZBFZFZ
      DGZPABFZDGZFZACBHZFZDGCUAHSPTFZDGUBRUEDAPBIJPTDKLUDRDUCQACBMNJCUAMO $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ch x $.
    $( Theorem *10.542 in [WhiteheadRussell] p. 156. $)
    pm10.542 $p |- ( A. x ( ph -> ( ch -> ps ) ) <->
        ( ch -> A. x ( ph -> ps ) ) ) $=
      ( wi wal bi2.04 albii 19.21v bitri ) ACBEEZDFCABEZEZDFCLDFEKMDACBGHCLDIJ
      $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

    $( Theorem *10.55 in [WhiteheadRussell] p. 156. $)
    pm10.55 $p |- ( ( E. x ( ph /\ ps ) /\ A. x ( ph -> ps ) ) <->
        ( E. x ph /\ A. x ( ph -> ps ) ) ) $=
      ( wa wex wi wal exan2 anim1i hba1 ancl a4s 19.22d impcom pm3.27 jca
      impbii ) ABDZCEZABFZCGZDACEZUADZSUBUAABCHIUCSUAUAUBSUAARCTCJTARFCABKLMNUB
      UAOPQ $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.56 in [WhiteheadRussell] p. 156. $)
    pm10.56 $p |- ( ( A. x ( ph -> ps ) /\
        E. x ( ph /\ ch ) ) -> E. x ( ps /\ ch ) ) $=
      ( wi wal wa wex wn pm3.45 con3d 19.20ii df-ex 3imtr4g imp ) ABEZDFZACGZDH
      ZBCGZDHZQRIZDFZITIZDFZISUAQUEUCPUDUBDPRTABCJKLKRDMTDMNO $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *10.57 in [WhiteheadRussell] p. 156. $)
    pm10.57 $p |- ( A. x ( ph -> ( ps \/ ch ) ) ->
        ( A. x ( ph -> ps ) \/ E. x ( ph /\ ch ) ) ) $=
      ( wo wi wal wa wex wn pm2.53 con1d imim3i imnan syl5ibr 19.20ii alnex
      orrd ) ABCEZFZDGZABFZDGZACHZDIZUAUEUCUAUDJZDGUCUEJTUFUBDTACJZFUBUFSUGBASB
      CBCKLMACNOPUDDQOLR $.
      $( [25-May-2011] $) $( [24-May-2011] $)

