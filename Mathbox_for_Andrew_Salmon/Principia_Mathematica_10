$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Principia Mathematica * 10
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d ph x $.
    $( Theorem *10.12 in [WhiteheadRussell] p. 146.  In *10, this is treated as
       an axiom, and the proofs in *10 are based on this theorem. $)
    pm10.12 $p |- ( A. x ( ph \/ ps ) -> ( ph \/ A. x ps ) ) $=
      ( wo wal ax-17 19.32 biimpi ) ABDCEABCEDABCACFGH $.
      $( [17-Jun-2011] $)
  $}

  $( Theorem *10.14 in [WhiteheadRussell] p. 146. $)
  pm10.14 $p |- ( ( A. x ph /\ A. x ps ) -> ( [ y / x ] ph /\ [ y / x ] ps )
    ) $=
    ( wal cv wsbc stdpc4 anim12i ) ACEACDFZGBCEBCJGACDHBCDHI $.
    $( [17-Jun-2011] $)

  $( Theorem *10.251 in [WhiteheadRussell] p. 149. $)
  pm10.251 $p |- ( A. x -. ph -> -. A. x ph ) $=
    ( wn wal wex alnex 19.2 con3i sylbi ) ACBDABEZCABDZCABFKJABBGHI $.
    $( [17-Jun-2011] $)

  $( Theorem *10.252 in [WhiteheadRussell] p. 149. $)
  pm10.252 $p |- ( -. E. x ph <-> A. x -. ph ) $=
    ( wn wal wex df-ex bicomi con1bii ) ACBDZABEZJICABFGH $.
    $( [17-Jun-2011] $)

  $( Theorem *10.253 in [WhiteheadRussell] p. 149. $)
  pm10.253 $p |- ( -. A. x ph <-> E. x -. ph ) $=
    ( wn wex wal alex bicomi con1bii ) ACBDZABEZJICABFGH $.
    $( [17-Jun-2011] $)

  $( Theorem *10.3 in [WhiteheadRussell] p. 150. $)
  alsyl $p |- ( ( A. x ( ph -> ps ) /\ A. x ( ps -> ch ) ) ->
        A. x ( ph -> ch ) ) $=
    ( wi pm3.33 alanimi ) ABEBCEACEDABCFG $.
    $( [8-Jun-2011] $)

  $( Theorem *10.301 in [WhiteheadRussell] p. 151. $)
  albitr $p |- ( ( A. x ( ph <-> ps ) /\ A. x ( ps <-> ch ) ) ->
        A. x ( ph <-> ch ) ) $=
    ( wb bitr alanimi ) ABEBCEACEDABCFG $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *10.42 in [WhiteheadRussell] p. 155. $)
  pm10.42 $p |- ( ( E. x ph \/ E. x ps ) <-> E. x ( ph \/ ps ) ) $=
    ( wo wex 19.43 bicomi ) ABDCEACEBCEDABCFG $.
    $( [17-Jun-2011] $)

  ${
    $d ps x $.
    $( Theorem *10.52 in [WhiteheadRussell] p. 155. $)
    pm10.52 $p |- ( E. x ph -> ( A. x ( ph -> ps ) <-> ps ) ) $=
      ( wex wi wal pm5.5 19.23v syl5bb ) ACDZJBEBABECFJBGABCHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  $( Theorem *10.53 in [WhiteheadRussell] p. 155. $)
  pm10.53 $p |- ( -. E. x ph -> A. x ( ph -> ps ) ) $=
    ( wex wn wal wi pm2.21 19.38 syl ) ACDZEKBCFZGABGCFKLHABCIJ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ch x $.
    $( Theorem *10.541 in [WhiteheadRussell] p. 155. $)
    pm10.541 $p |- ( A. x ( ph -> ( ch \/ ps ) ) <->
        ( ch \/ A. x ( ph -> ps ) ) ) $=
      ( wn wi wal wo bi2.04 albii 19.21v bitri df-or imbi2i 3bitr4i ) ACEZBFZFZ
      DGZPABFZDGZFZACBHZFZDGCUAHSPTFZDGUBRUEDAPBIJPTDKLUDRDUCQACBMNJCUAMO $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ch x $.
    $( Theorem *10.542 in [WhiteheadRussell] p. 156. $)
    pm10.542 $p |- ( A. x ( ph -> ( ch -> ps ) ) <->
        ( ch -> A. x ( ph -> ps ) ) ) $=
      ( wi wal bi2.04 albii 19.21v bitri ) ACBEEZDFCABEZEZDFCLDFEKMDACBGHCLDIJ
      $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  $( Theorem *10.55 in [WhiteheadRussell] p. 156. $)
  pm10.55 $p |- ( ( E. x ( ph /\ ps ) /\ A. x ( ph -> ps ) ) <->
        ( E. x ph /\ A. x ( ph -> ps ) ) ) $=
    ( wa wex wi wal exsimpl anim1i hba1 ancl a4s eximd impcom simpr jca impbii
    ) ABDZCEZABFZCGZDACEZUADZSUBUAABCHIUCSUAUAUBSUAARCTCJTARFCABKLMNUBUAOPQ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *10.56 in [WhiteheadRussell] p. 156. $)
  pm10.56 $p |- ( ( A. x ( ph -> ps ) /\
        E. x ( ph /\ ch ) ) -> E. x ( ps /\ ch ) ) $=
    ( wi wal wa wex wn pm3.45 con3d al2imi df-ex 3imtr4g imp ) ABEZDFZACGZDHZBC
    GZDHZQRIZDFZITIZDFZISUAQUEUCPUDUBDPRTABCJKLKRDMTDMNO $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *10.57 in [WhiteheadRussell] p. 156. $)
  pm10.57 $p |- ( A. x ( ph -> ( ps \/ ch ) ) ->
        ( A. x ( ph -> ps ) \/ E. x ( ph /\ ch ) ) ) $=
    ( wo wi wal wa wex wn pm2.53 con1d imim3i imnan syl5ibr al2imi alnex orrd
    ) ABCEZFZDGZABFZDGZACHZDIZUAUEUCUAUDJZDGUCUEJTUFUBDTACJZFUBUFSUGBASBCBCKLMA
    CNOPUDDQOLR $.
    $( [25-May-2011] $) $( [24-May-2011] $)

