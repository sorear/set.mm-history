$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Predicate Calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z $.
    $( If ` x = y ` always implies ` x = z ` , then ` y = z ` is true.
       (Contributed by Andrew Salmon, 2-Jun-2011.) $)
    sbeqal1 $p |- ( A. x ( x = y -> x = z ) -> y = z ) $=
      ( cv wceq wi wal wsbc sb2 equsb3 sylib ) ADZBDZELCDZEZFAGOAMHMNEOABIBACJK
      $.
      $( [2-Jun-2011] $)
  $}

  ${
    $d x z $.
    sbeqal1i.1 $e |- ( x = y -> x = z ) $.
    $( Suppose you know ` x = y ` implies ` x = z ` , assuming ` x ` and ` z `
       are distinct.  Then, ` y = z ` .  (Contributed by Andrew Salmon,
       3-Jun-2011.) $)
    sbeqal1i $p |- y = z $=
      ( cv wceq wi sbeqal1 mpg ) AEZBEZFJCEZFGKLFAABCHDI $.
      $( [3-Jun-2011] $)

    $( If ` x = y ` implies ` x = z ` , then we can infer ` z = y ` .
       (Contributed by Andrew Salmon, 3-Jun-2011.) $)
    sbeqal2i $p |- z = y $=
      ( cv sbeqal1i eqcomi ) BECEABCDFG $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d y z $.  $d x z $.
    $( When both ` x ` and ` z ` and ` y ` and ` z ` are both distinct, then
       the converse of sbeqal1 holds as well.  (Contributed by Andrew Salmon,
       2-Jun-2011.) $)
    sbeqalbi $p |- ( x = y <-> A. z ( z = x -> z = y ) ) $=
      ( cv wceq wi wal equtrr 19.21aiv sbeqal1 impbii ) ADZBDZEZCDZLEOMEFZCGNPC
      ABCHICABJK $.
      $( [2-Jun-2011] $)
  $}

  $( Proof of a theorem that can act as a sole axiom for pure predicate
     calculus with ~ ax-gen as the inference rule.  This proof extends the idea
     of ~ ax467 and related theorems.  (Contributed by Andrew Salmon,
     14-Jul-2011.) $)
  ax4567 $p |- ( ( A. x A. y -. A. x A. y ( A. y ph -> ps ) -> ( ph ->
      A. y ( A. y ph -> ps ) ) ) -> ( A. y ph -> A. y ps ) ) $=
    ( wal wi wn ax-6 ax-6o con1i alimi ax-7 3syl ax-5o syl pm2.21 a4sd ja ) ADE
    ZBFZDEZCEGZDECEZAUAFSBDEZFZUCGUAUEUAUCUAGZUFDEUBCEZDEUCTDHUFUGDUGUAUACIJKUB
    DCLMJABDNZOAUAUEAGAUDDAUDPQUHRR $.
    $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-4 from ~ ax4567 .  Note that ~ ax-9 is used for the
     re-derivation.  (Contributed by Andrew Salmon, 14-Jul-2011.) $)
  ax4567to4 $p |- ( A. x ph -> ph ) $=
    ( vy wal wn cv wceq ax-9 wi pm2.21 ax-1 ax4567 syl con3d mpi con4i ) AABDZA
    EZBFCFGEZBDZEQEBCHRQTRAQSIBDZIZQTIZAUAJUBUABDEBDBDZUBIUCUBUDKASBBLMMNOP $.
    $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-5o from ~ ax4567 .  Note that only propositional
     calculus is required for the re-derivation.  (Contributed by Andrew
     Salmon, 14-Jul-2011.) $)
  ax4567to5 $p |- ( A. x ( A. x ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wal wi wn ax-1 ax4567 3syl ) ACDZBECDZAKEZKCDFCDCDZLEJBCDEKAGLMGABCCHI $.
    $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-6o from ~ ax4567 .  Note that neither ~ ax-6o nor
     ~ ax-7 are required for the re-derivation.  (Contributed by Andrew Salmon,
     14-Jul-2011.) $)
  ax4567to6 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn wi ax-1 alimi a5i con3i a4s pm2.21 ax4567 syl ax-4 syl6 pm2.27 mpg
    id 3syl ) ABCZDZBCZDAAEZBCZAEZBCZBCZDZBCZBCZDZUEAUJUBUIUBBUHUABTUGAUFBAUEBA
    UDFGHIGJIUKUDTAUKUJUCUFEZEUDTEUJULKUCABBLMABNOUCUEAEBUDAPARQS $.
    $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-7 from ~ ax4567 .  Note that ~ ax-7 is not required
     for the re-derivation.  (Contributed by Andrew Salmon, 14-Jul-2011.) $)
  ax4567to7 $p |- ( A. x A. y ph -> A. y A. x ph ) $=
    ( wal wi ax-1 2alimi wn ax4567to6 con4i pm2.21 ax4567 ax-4 syl6 alimi nsyl4
    syl pm2.27 id mpg 3syl ) ACDZBDAAEZCDZAEZCDZBDZUEBDZCDZABDCDAUEBCAUDFGUGUGH
    ZCDZHZCDZUIUMUGUJCIJULUHCUKBDZHZBDUHUKUOUEBUOUNUCUFEZEZUEUNUPKUQUDUBAUCABCL
    ACMNQOUKBIPOQUEACBUCUEAECUDARASTGUA $.
    $( [14-Jul-2011] $)

  ${
    $d x z w $.
    $( This theorem shows that, given ~ axext4 , ~ ax-10 is logically
       redundant.  (Contributed by Andrew Salmon, 16-Jul-2011.) $)
    ax10ext $p |- ( A. x x = z -> A. z z = x ) $=
      ( vw cv wcel wb wal wceq wi alimi ax-17 syl ax-14 syl5 a5i wex hba1 19.23
      19.8a imim12i ax-ext bi2 stdpc5 idd imim12d alimdv a4s mpcom elequ2 cbvex
      ax-7 sylib cbv3 sylbi a7s 19.21aiv ax-4 impbid 3syl axext4 albii 3imtr4i
      ) CDZADZEZVCBDZEZFZCGZAGZVGVEFZCGZBGZVDVFHZAGZVFVDHZBGVJVEVEAGZIZCGZAGZVG
      VGBGZIZCGZBGVMVIVSAVOVJVSVIVNAABCUAJVNVJVSIAVJVHAGZCGVNVSVHACUKVNWDVRCWDV
      GVQIZVNVRWDVGVEIZAGWEVHWFAVEVGUBJVGVEAVGAKZUCLVNVEVGVQVQABCMZVNVQUDUENUFN
      UGUHOVTWCBVRWCCAVRAGZWBCWIVEAPZVQIWBVEVQAVEAQRVGWJVQWAVGVGBPZWJVGBSZVGVEB
      AWGVEBKZBACUIUJULVEVGABWMWGWHUMTUNJUOUPWCVLBWBVLCBWBBGZVKCWNWKWAIZVKVGWAB
      VGBQRWOVGVEVGWKWAVEWLWAVQVEVGVEBAWGWMBACMUMVEAUQLTVEWKWAVGVEWJWKVEASVEVGA
      BWMWGABCUIUJULVGBUQTURUNJUOOUSVNVIAABCUTVAVPVLBBACUTVAVB $.
      $( [16-Jul-2011] $)

    $( This theorem shows that, given ~ ax-16 , ~ ax-10 is logically
       redundant.  (Contributed by Andrew Salmon, 27-Jul-2011.) $)
    ax10-16 $p |- ( A. x x = z -> A. z z = x ) $=
      ( vw cv wal wi ax-16 19.21aiv a5i equequ1 wb cbvalv imbi12d albidv biimpi
      wceq a1i wex sylbi a7s hba1 19.23 albii a9e pm2.27 ax-mp equequ2 ax-4 a4s
      alimi syl 3syl ) ADZBDZPZAEZUMCDZPZURAEZFZCEZAEZUNUQPZVCBEZFZCEZBEZUNUMPZ
      BEUOVAAUPUTCURABGHIVBVGVAVFABUOUTVECUOURVCUSVDABCJZUSVDKUOURVCABVILQMNLOV
      FVHBVEVHCBVEBEZCEVCBRZVDFZCEZVHVJVLCVCVDBVCBUAUBUCVMVDCEVHVLVDCVKVLVDFBCU
      DVKVDUEUFUJVCVHBCVCCEZVHBVNVHAEVHVCVHCACABUGLVHAUHSUITUKSTIUL $.
      $( [27-Jul-2011] $)
  $}

