$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Predicate Calculus
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z $.
    $( If ` x = y ` always implies ` x = z ` , then ` y = z ` is true. $)
    sbeqal1 $p |- ( A. x ( x = y -> x = z ) -> y = z ) $=
      ( cv wceq wi wal wsbc sb2 equsb3 sylib ) ADZBDZELCDZEZFAGOAMHMNEOABIBACJK
      $.
      $( [2-Jun-2011] $) $( [2-Jun-2011] $)
  $}

  ${
    $d x z $.
    sbeqal1i.1 $e |- ( x = y -> x = z ) $.
    $( Suppose you know ` x = y ` implies ` x = z ` , assuming ` x ` and ` z `
       are distinct.  Then, ` y = z ` . $)
    sbeqal1i $p |- y = z $=
      ( cv wceq wi sbeqal1 mpg ) AEZBEZFJCEZFGKLFAABCHDI $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( If ` x = y ` implies ` x = z ` , then we can infer ` z = y ` . $)
    sbeqal2i $p |- z = y $=
      ( cv sbeqal1i eqcomi ) BECEABCDFG $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d y z $.  $d x z $.
    $( When both ` x ` and ` z ` and ` y ` and ` z ` are both distinct, then
       the converse of sbeqal1 holds as well. $)
    sbeqalbi $p |- ( x = y <-> A. z ( z = x -> z = y ) ) $=
      ( cv wceq wi wal equtrr 19.21aiv sbeqal1 impbii ) ADZBDZEZCDZLEOMEFZCGNPC
      ABCHICABJK $.
      $( [2-Jun-2011] $) $( [2-Jun-2011] $)
  $}

  $( Proof of a theorem that can act as a sole axiom for pure predicate
     calculus with ~ ax-gen as the inference rule.  This proof extends the idea
     of ~ ax467 and related theorems. $)
  ax4567 $p |- ( ( A. x A. y -. A. x A. y ( A. y ph -> ps ) -> ( ph ->
      A. y ( A. y ph -> ps ) ) ) -> ( A. y ph -> A. y ps ) ) $=
    ( wal wi wn ax-6 ax-6o con1i 19.20i ax-7 3syl ax-5o syl pm2.21 a4sd ja ) AD
    EZBFZDEZCEGZDECEZAUAFSBDEZFZUCGUAUEUAUCUAGZUFDEUBCEZDEUCTDHUFUGDUGUAUACIJKU
    BDCLMJABDNZOAUAUEAGAUDDAUDPQUHRR $.
    $( [14-Jul-2011] $) $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-4 from ~ ax4567 .  Note that ~ ax-9 is used for the
     re-derivation. $)
  ax4567to4 $p |- ( A. x ph -> ph ) $=
    ( vy wal wn cv wceq ax-9 wi pm2.21 ax-1 ax4567 syl con3d mpi con4i ) AABDZA
    EZBFCFGEZBDZEQEBCHRQTRAQSIBDZIZQTIZAUAJUBUABDEBDBDZUBIUCUBUDKASBBLMMNOP $.
    $( [15-Jul-2011] $) $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-5o from ~ ax4567 .  Note that only propositional
     calculus is required for the re-derivation. $)
  ax4567to5 $p |- ( A. x ( A. x ph -> ps ) -> ( A. x ph -> A. x ps ) ) $=
    ( wal wi wn ax-1 ax4567 3syl ) ACDZBECDZAKEZKCDFCDCDZLEJBCDEKAGLMGABCCHI $.
    $( [15-Jul-2011] $) $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-6o from ~ ax4567 .  Note that neither ~ ax-6o nor
     ~ ax-7 are required for the re-derivation. $)
  ax4567to6 $p |- ( -. A. x -. A. x ph -> ph ) $=
    ( wal wn wi ax-1 19.20i a5i con3i a4s pm2.21 ax4567 syl ax-4 syl6 pm2.27
    id mpg 3syl ) ABCZDZBCZDAAEZBCZAEZBCZBCZDZBCZBCZDZUEAUJUBUIUBBUHUABTUGAUFBA
    UEBAUDFGHIGJIUKUDTAUKUJUCUFEZEUDTEUJULKUCABBLMABNOUCUEAEBUDAPAQRS $.
    $( [15-Jul-2011] $) $( [14-Jul-2011] $)

  $( Re-derivation of ~ ax-7 from ~ ax4567 .  Note that ~ ax-7 is not required
     for the re-derivation. $)
  ax4567to7 $p |- ( A. x A. y ph -> A. y A. x ph ) $=
    ( wal wi ax-1 19.20i2 wn ax4567to6 con4i pm2.21 ax4567 ax-4 syl6 syl
    19.20i nsyl4 pm2.27 id mpg 3syl ) ACDZBDAAEZCDZAEZCDZBDZUEBDZCDZABDCDAUEBCA
    UDFGUGUGHZCDZHZCDZUIUMUGUJCIJULUHCUKBDZHZBDUHUKUOUEBUOUNUCUFEZEZUEUNUPKUQUD
    UBAUCABCLACMNOPUKBIQPOUEACBUCUEAECUDARASTGUA $.
    $( [15-Jul-2011] $) $( [14-Jul-2011] $)

  ${
    $d x z w $.
    $( This theorem shows that, given ~ axext4 , ~ ax-10 is logically
       redundant. $)
    ax10ext $p |- ( A. x x = z -> A. z z = x ) $=
      ( vw cv wcel wb wal wceq wi ax-ext 19.20i ax-14 idd imim12d bi2 ax-17
      stdpc5 syl syl5 19.20dv ax-7 a4s mpcom a5i wex hba1 19.23 19.8a elequ2
      cbvex sylib cbv3 imim12i sylbi a7s 19.21aiv ax-4 impbid 3syl axext4
      albii 3imtr4i ) CDZADZEZVCBDZEZFZCGZAGZVGVEFZCGZBGZVDVFHZAGZVFVDHZBGVJVEV
      EAGZIZCGZAGZVGVGBGZIZCGZBGVMVIVSAVOVJVSVIVNAABCJKVNVJVSIAVNVHAGZCGVSVJVNW
      DVRCVNVGVQIZVRWDVNVEVGVQVQABCLZVNVQMNWDVGVEIZAGWEVHWGAVEVGOKVGVEAVGAPZQRS
      TVHACUASUBUCUDVTWCBVRWCCAVRAGZWBCWIVEAUEZVQIWBVEVQAVEAUFUGVGWJVQWAVGVGBUE
      ZWJVGBUHZVGVEBAWHVEBPZBACUIUJUKVEVGABWMWHWFULUMUNKUOUPWCVLBWBVLCBWBBGZVKC
      WNWKWAIZVKVGWABVGBUFUGWOVGVEVGWKWAVEWLWAVQVEVGVEBAWHWMBACLULVEAUQRUMVEWKW
      AVGVEWJWKVEAUHVEVGABWMWHABCUIUJUKVGBUQUMURUNKUOUDUSVNVIAABCUTVAVPVLBBACUT
      VAVB $.
      $( [16-Jul-2011] $) $( [16-Jul-2011] $)

    $( This theorem shows that, given ~ ax-16 , ~ ax-10 is logically
       redundant. $)
    ax10-16 $p |- ( A. x x = z -> A. z z = x ) $=
      ( vw cv wceq wal wi ax-16 19.21aiv a5i equequ1 wb cbvalv a1i imbi12d
      albidv biimpi wex hba1 19.23 albii a9e pm2.27 ax-mp 19.20i equequ2 ax-4
      sylbi a4s a7s syl 3syl ) ADZBDZEZAFZUMCDZEZURAFZGZCFZAFZUNUQEZVCBFZGZCFZB
      FZUNUMEZBFUOVAAUPUTCURABHIJVBVGVAVFABUOUTVECUOURVCUSVDABCKZUSVDLUOURVCABV
      IMNOPMQVFVHBVEVHCBVEBFZCFVCBRZVDGZCFZVHVJVLCVCVDBVCBSTUAVMVDCFVHVLVDCVKVL
      VDGBCUBVKVDUCUDUEVCVHBCVCCFZVHBVNVHAFVHVCVHCACABUFMVHAUGUHUIUJUKUHUJJUL
      $.
      $( [27-Jul-2011] $) $( [27-Jul-2011] $)
  $}

  ${
    $d x z $.  $d y z $.
    dvelimfALT2.1 $e |- ( ph -> A. x ph ) $.
    dvelimfALT2.2 $e |- ( ps -> A. z ps ) $.
    dvelimfALT2.3 $e |- ( z = y -> ( ph <-> ps ) ) $.
    $( Proof of ~ dvelimf using ~ dveeq2 instestad of ~ ax-12 .  This shows
       that ~ ax-12 could be replaced by ~ dveeq2 . $)
    dvelimfALT2 $p |- ( -. A. x x = y -> ( ps -> A. x ps ) ) $=
      ( cv wceq wal wn wi ax-17 hbn1 dveeq2 a1i hbimd hbald equsal albii
      3imtr3g ) CIDIZJZCKLZEIUCJZAMZEKZUHCKBBCKUEUGCEUEENUEUFACUDCOCDEPAACKMUEF
      QRSABEDGHTZUHBCUIUAUB $.
      $( [25-Jul-2011] $) $( [21-Jul-2011] $)
  $}

  ${
    $d w x $.  $d w y $.  $d w z $.
    $( Reprove ~ ax-12 using ~ dvelimfALT2 . $)
    ax12 $p |- ( -. A. x x = y -> ( -. A. x x = z -> ( y = z -> A. x y = z ) )
        ) $=
      ( vw cv wceq wal wn wi hbn1 dveeq2 hbim1 ax-17 equequ1 imbi2d
      dvelimfALT2 19.21 syl6ib pm2.86d ) AEZBEZFAGHZTCEZFZAGHZUAUCFZUFAGZUBUEUF
      IZUHAGUEUGIUEDEZUCFZIUHABDUEUJAUDAJZACDKLUHDMUIUAFUJUFUEDBCNOPUEUFAUKQRS
      $.
      $( [25-Jul-2011] $) $( [21-Jul-2011] $)
  $}

