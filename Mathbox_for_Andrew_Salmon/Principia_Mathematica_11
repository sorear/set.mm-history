$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Principia Mathematica * 11
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    19.20ian2.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( Removes two universal quantifiers from a statement.  This theorem does
       not actually appear in Principia Mathematica, but is useful for
       shortening proofs. $)
    19.20ian2 $p |- ( ( A. x A. y ph /\ A. x A. y ps ) -> A. x A. y ch ) $=
      ( wal 19.20ian ) AEGBEGCEGDABCEFHH $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    19.20ii2.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Removes two universal qunatifiers from a statement.  Useful for
       shortening proofs in this chapter. $)
    19.20ii2 $p |- ( A. x A. y ph -> ( A. x A. y ps -> A. x A. y ch ) ) $=
      ( wal 19.20ii ) AEGBEGCEGDABCEFHH $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph x y z $.  $d w x z $.
    $( Theorem *11.07 in [WhiteheadRussell] p. 159. $)
    pm11.07 $p |- ( [ w / x ] [ y / z ] ph <-> [ y / x ] [ w / z ] ph ) $=
      ( cv wceq wa wex wsbc a9e pm3.2i 2th eeanv 3bitr4i anbi1i 19.41vv 2sb5 ) 
      BFZEFZGZDFZCFZGZHZAHDIBIZSUCGZUBTGZHZAHDIBIZADUCJBTJADTJBUCJUEDIBIZAHUIDI
      BIZAHUFUJUKULAUABIZUDDIZHZUGBIZUHDIZHZUKULUOURUMUNBEKDCKLUPUQBCKDEKLMUAUD
      BDNUGUHBDNOPUEABDQUIABDQOABDECRABDCERO $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)
  $}

    $( Theorem *11.1 in [WhiteheadRussell] p. 159. $)
    pm11.1 $p |- ( A. x A. y ph -> [ z / x ] [ w / y ] ph ) $=
      ( wal wsb stdpc4 19.20i syl ) ACFZBFACEGZBFLBDGKLBACEHILBDHJ $.
      $( [17-Jun-2011] $) $( [24-May-2011] $)

  ${
    pm11.11.1 $e |- ph $.
    $( Theorem *11.11 in [WhiteheadRussell] p. 159. $)
    pm11.11 $p |- A. z A. w [ z / x ] [ w / y ] ph $=
      ( wsb wal pm11.1 ax-gen mpg gen2 ) ACEGBDGZDEACHMBABCDEIACFJKL $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $d ph x $.  $d ph y $.
    $( Theorem *11.12 in [WhiteheadRussell] p. 159. $)
    pm11.12 $p |- ( A. x A. y ( ph \/ ps ) -> ( ph \/ A. x A. y ps ) ) $=
      ( wo wal pm10.12 19.20i syl ) ABEDFZCFABDFZEZCFAKCFEJLCABDGHAKCGI $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)
  $}

    $( Theorem *11.14 in [WhiteheadRussell] p. 159. $)
    pm11.14 $p |- ( ( A. x A. y ph /\ A. x A. y ps ) ->
                  ( [ z / x ] [ w / y ] ph /\ [ z / x ] [ w / y ] ps ) ) $=
      ( wal wsb pm11.1 anim12i ) ADGCGADFHCEHBDGCGBDFHCEHACDEFIBCDEFIJ $.
      $( [17-Jun-2011] $) $( [24-May-2011] $)

    $( Theorem *11.21 in [WhiteheadRussell] p. 160. $)
    pm11.21 $p |- ( A. x A. y A. z ph <-> A. y A. z A. x ph ) $=
      ( wal alcom albii bitri ) ADEZCEBEIBEZCEABEDEZCEIBCFJKCABDFGH $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.22 in [WhiteheadRussell] p. 160. $)
    pm11.22 $p |- ( E. x E. y ph <-> -. A. x A. y -. ph ) $=
      ( wex wn wal df-ex alnex albii notbii bitr4i ) ACDZBDLEZBFZEAECFZBFZELBGP
      NOMBACHIJK $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.25 in [WhiteheadRussell] p. 160. $)
    pm11.25 $p |- ( -. E. x E. y ph <-> A. x A. y -. ph ) $=
      ( wn wal wex pm11.22 bicomi con1bii ) ADCEBEZACFBFZKJDABCGHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ps x $.    $d ps y $.
    $( Theorem *11.3 in [WhiteheadRussell] p. 161. $)
    pm11.3 $p |- ( ( ps -> A. x A. y ph ) <-> A. x A. y ( ps -> ph ) ) $=
      ( wi wal 19.21v albii bitr2i ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

    $( Theorem *11.31 in [WhiteheadRussell] p. 161. $)
    pm11.31 $p |- ( ( A. x A. y ph /\ A. x A. y ps ) <->
                  A. x A. y ( ph /\ ps ) ) $=
      ( wa wal 19.26-2 bicomi ) ABEDFCFADFCFBDFCFEABCDGH $.
      $( [17-Jun-2011] $) $( [17-Jun-2011] $)

    $( Theorem *11.32 in [WhiteheadRussell] p. 162. $)
    pm11.32 $p |- ( A. x A. y ( ph -> ps ) ->
        ( A. x A. y ph -> A. x A. y ps ) ) $=
      ( wi id 19.20ii2 ) ABEZABCDHFG $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.33 in [WhiteheadRussell] p. 162. $)
    pm11.33 $p |- ( A. x A. y ( ph <-> ps ) ->
        ( A. x A. y ph <-> A. x A. y ps ) ) $=
      ( wb wal hba1 19.15 a4s albid ) ABEDFZCFADFZBDFZCKCGKLMECABDHIJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.34 in [WhiteheadRussell] p. 162. $)
    pm11.34 $p |- ( A. x A. y ( ph -> ps ) ->
        ( E. x E. y ph -> E. x E. y ps ) ) $=
      ( wi wal wex 19.22 19.20i syl ) ABEDFZCFADGZBDGZEZCFLCGMCGEKNCABDHILMCHJ
      $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.341 in [WhiteheadRussell] p. 162. $)
    pm11.341 $p |- ( A. x A. y ( ph <-> ps ) ->
        ( E. x E. y ph <-> E. x E. y ps ) ) $=
      ( wb wal wex 19.18 19.20i syl ) ABEDFZCFADGZBDGZEZCFLCGMCGEKNCABDHILMCHJ
      $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.36 in [WhiteheadRussell] p. 162. $)
    pm11.36 $p |- ( [ z / x ] [ w / y ] ph -> E. x E. y ph ) $=
      ( wsb wex a4sbe 19.22i syl ) ACEFZBDFKBGACGZBGKBDHKLBACEHIJ $.
      $( [17-Jun-2011] $) $( [24-May-2011] $)

    $( Theorem *11.37 in [WhiteheadRussell] p. 162. $)
    pm11.37 $p |- ( ( A. x A. y ( ph -> ps ) /\ A. x A. y ( ps -> ch ) ) ->
                  A. x A. y ( ph -> ch ) ) $=
      ( wi pm3.33 19.20ian2 ) ABFBCFACFDEABCGH $.
      $( [25-May-2011] $) $( [26-May-2011] $)

    $( Theorem *11.371 in [WhiteheadRussell] p. 163. $)
    pm11.371 $p |- ( ( A. x A. y ( ph <-> ps ) /\ A. x A. y ( ps <-> ch ) ) ->
                   A. x A. y ( ph <-> ch ) ) $=
      ( wb bitr 19.20ian2 ) ABFBCFACFDEABCGH $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.38 in [WhiteheadRussell] p. 163. $)
    pm11.38 $p |- ( A. x A. y ( ph -> ps ) -> ( A. x A. y ( ph /\ ch ) ->
                  A. x A. y ( ps /\ ch ) ) ) $=
      ( wi wa pm3.45 19.20ii2 ) ABFACGBCGDEABCHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.39 in [WhiteheadRussell] p. 163. $)
    pm11.39 $p |- ( ( A. x A. y ( ph -> ps ) /\ A. x A. y ( ch -> th ) ) ->
        ( A. x A. y ( ph /\ ch ) -> A. x A. y ( ps /\ th ) ) ) $=
      ( wi wal wa id 19.20ian2 prth 19.20ii2 syl ) ABGZFHEHCDGZFHEHIOPIZFHEHACI
      ZFHEHBDIZFHEHGOPQEFQJKQRSEFABCDLMN $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.391 in [WhiteheadRussell] p. 163. $)
    pm11.391 $p |- ( ( A. x A. y ( ph -> ps ) /\ A. x A. y ( ph -> ch ) ) <->
        A. x A. y ( ph -> ( ps /\ ch ) ) ) $=
      ( wi wal wa 19.26-2 pm4.76 2albii bitr3i ) ABFZEGDGACFZEGDGHMNHZEGDGABCHF
      ZEGDGMNDEIOPDEABCJKL $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.4 in [WhiteheadRussell] p. 163. $)
    pm11.4 $p |- ( ( A. x A. y ( ph <-> ps ) /\ A. x A. y ( ch <-> th ) ) ->
                  A. x A. y ( ( ph /\ ch ) <-> ( ps /\ th ) ) ) $=
      ( wb wa pm4.38 19.20ian2 ) ABGCDGACHBDHGEFACBDIJ $.
      $( [25-May-2011] $) $( [25-May-2011] $)

    $( Theorem *11.401 in [WhiteheadRussell] p. 163. $)
    pm11.401 $p |- ( A. x A. y ( ph <-> ps ) ->
                   A. x A. y ( ( ph /\ ch ) <-> ( ps /\ ch ) ) ) $=
      ( wb wa anbi1 19.20i2 ) ABFACGBCGFDEABCHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.41 in [WhiteheadRussell] p. 163. $)
    pm11.41 $p |- ( ( E. x E. y ph \/ E. x E. y ps ) <->
        E. x E. y ( ph \/ ps ) ) $=
      ( wo wex 19.43 exbii bitr2i ) ABEDFZCFADFZBDFZEZCFKCFLCFEJMCABDGHKLCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.42 in [WhiteheadRussell] p. 163. $)
    pm11.42 $p |- ( E. x E. y ( ph /\ ps ) ->
        ( E. x E. y ph /\ E. x E. y ps ) ) $=
      ( wa wex 19.40 19.22i syl ) ABEDFZCFADFZBDFZEZCFKCFLCFEJMCABDGHKLCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.421 in [WhiteheadRussell] p. 163. $)
    pm11.421 $p |- ( ( A. x A. y ph \/ A. x A. y ps ) ->
        A. x A. y ( ph \/ ps ) ) $=
      ( wal wo orc 19.20i2 olc jaoi ) ADECEABFZDECEBDECEAKCDABGHBKCDBAIHJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ps x $.    $d ps y $.
    $( Theorem *11.43 in [WhiteheadRussell] p. 163. $)
    pm11.43 $p |- ( E. x E. y ( ph -> ps ) <-> ( A. x A. y ph -> ps ) )  $=
      ( wi wex wal 19.36v exbii bitri ) ABEDFZCFADGZBEZCFLCGBEKMCABDHILBCHJ $.
      $( [25-May-2011] $) $( [25-May-2011] $)
  $}

  ${
    $d ps x $.    $d ps y $.
    $( Theorem *11.44 in [WhiteheadRussell] p. 163. $)
    pm11.44 $p |- ( A. x A. y ( ph \/ ps ) <-> ( A. x A. y ph \/ ps ) ) $=
      ( wo wal ax-17 19.31 albii bitri ) ABEDFZCFADFZBEZCFLCFBEKMCABDBDGHILBCBC
      GHJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ps x $.    $d ps y $.
    $( Theorem *11.46 in [WhiteheadRussell] p. 164. $)
    pm11.46 $p |- ( E. x E. y ( ps -> ph ) <-> ( ps -> E. x E. y ph ) ) $=
      ( wi wex 19.37v exbii bitri ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ps x $.    $d ps y $.
    $( Theorem *11.47 in [WhiteheadRussell] p. 164. $)
    pm11.47 $p |- ( A. x A. y ( ps /\ ph ) <-> ( ps /\ A. x A. y ph ) ) $=
      ( wa wal 19.28v albii bitri ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

    $( Part of theorem *11.5 in [WhiteheadRussell] p. 164. $)
    pm11.5a $p |- ( E. x -. A. y ph <-> -. A. x A. y ph ) $=
      ( wal exnal ) ACDBE $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Part of theorem *11.5 in [WhiteheadRussell] p. 164. $)
    pm11.5b $p |- ( -. A. x A. y ph <-> E. x E. y -. ph ) $=
      ( wal wn wex alex albii notbii df-ex bitr4i ) ACDZBDZEAECFZEZBDZENBFMPLOB
      ACGHINBJK $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Part of theorem *11.5 in [WhiteheadRussell] p. 164. $)
    pm11.5c $p |- ( E. x -. A. y ph <-> E. x E. y -. ph ) $=
      ( wal wn wex pm11.5a pm11.5b bitri ) ACDZEBFJBDEAECFBFABCGABCHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.51 in [WhiteheadRussell] p. 164. $)
    pm11.51 $p |- ( E. x A. y ph <-> -. A. x E. y -. ph ) $=
      ( wal wex wn df-ex exnal albii notbii bitr4i ) ACDZBELFZBDZFAFCEZBDZFLBGP
      NOMBACHIJK $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.52 in [WhiteheadRussell] p. 164. $)
    pm11.52 $p |- ( E. x E. y ( ph /\ ps ) <->
        -. A. x A. y ( ph -> -. ps ) ) $=
      ( wa wex wn wi wal df-an 2exbii pm11.5b bitr4i ) ABEZDFCFABGHZGZDFCFODICI
      GNPCDABJKOCDLM $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.521 in [WhiteheadRussell] p. 164. $)
    pm11.521 $p |- ( -. E. x E. y ( ph /\ -. ps ) <->
        A. x A. y ( ph -> ps ) ) $=
      ( wn wa wex wi wal annim 2exbii notbii pm11.5b con1bii bitri ) ABEFZDGCGZ
      EABHZEZDGCGZERDICIZQTPSCDABJKLUATRCDMNO $.
      $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ph y $.    $d ps x $.
    $( Theorem *11.53 in [WhiteheadRussell] p. 164. $)
    pm11.53 $p |- ( A. x A. y ( ph -> ps ) <-> ( E. x ph -> A. y ps ) ) $=
      ( wi wal wex 19.21v albii ax-17 hbal 19.23 bitri ) ABEDFZCFABDFZEZCFACGOE
      NPCABDHIAOCBCDBCJKLM $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.    $d ps x $.
    $( Theorem *11.56 in [WhiteheadRussell] p. 165.  This theorem is similar
       to eeanv but uses universal quantifiers, not existential quantifiers.
       $)
    pm11.56 $p |- ( ( A. x ph /\ A. y ps ) <-> A. x A. y ( ph /\ ps ) ) $=
      ( wa wal ax-17 aaan bicomi ) ABEDFCFACFBDFEABCDADGBCGHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.57 in [WhiteheadRussell] p. 165. $)
    pm11.57 $p |- ( A. x ph <-> A. x A. y ( ph /\ [ y / x ] ph ) ) $=
      ( wal wsb wa ax-17 hbal ax4 stdpc4 jca 19.21ai a5i pm3.26 a4s 19.20i 
      impbii ) ABDZAABCEZFZCDZBDAUABRTCACBACGHRASABIABCJKLMUAABTACASNOPQ $.
      $( [8-Jun-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.58 in [WhiteheadRussell] p. 165. $)
    pm11.58 $p |- ( E. x ph <-> E. x E. y ( ph /\ [ y / x ] ph ) ) $=
      ( cv wsbc wa wex id 19.8a ax-17 sb8e biimpi syl jca pm3.26 impbii 19.42v 
      bitr4i exbii ) AAABCDEZFCGZBAATCGZFZUAAUCAAUBAHAABGZUBABIUDUBABCACJKLMNAU
      BOPATCQRS $.
      $( [8-Jun-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.    $d ps y $.
    $( Theorem *11.59 in [WhiteheadRussell] p. 165. $)
    pm11.59 $p |- ( A. x ( ph -> ps ) ->
        A. y A. x ( ( ph /\ [ y / x ] ph ) -> ( ps /\ [ y / x ] ps ) ) ) $=
      ( wi wal wsb wa ax-17 hbal ax4 a4sbim anim12d a5i 19.21ai ) ABEZCFZAACDGZ
      HBBCDGZHEZCFDPDCPDIJPTCQABRSPCKABCDLMNO $.
      $( [8-Jun-2011] $) $( [25-May-2011] $)
  $}

  ${
    $d ps x $.    $d ch y $.
    $( Theorem *11.6 in [WhiteheadRussell] p. 165. $)
    pm11.6 $p |- ( E. x ( E. y ( ph /\ ps ) /\ ch ) <->
        E. y ( E. x ( ph /\ ch ) /\ ps ) ) $=
      ( wa wex excom an23 2exbii bitri 19.41v exbii 3bitr3i ) ABFZCFZEGZDGZACFZ
      BFZDGZEGZOEGCFZDGSDGBFZEGRPDGEGUBPDEHPTEDABCIJKQUCDOCELMUAUDESBDLMN $.
      $( [25-May-2011] $) $( [25-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.61 in [WhiteheadRussell] p. 166. $)
    pm11.61 $p |- ( E. y A. x ( ph -> ps ) -> A. x ( ph -> E. y ps ) ) $=
      ( wi wal wex 19.12 19.37v biimpi 19.20i syl ) ABEZCFDGMDGZCFABDGEZCFMDCHN
      OCNOABDIJKL $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.62 in [WhiteheadRussell] p. 166.  Importation combined with
       the rearrangement with quantifiers. $)
    pm11.62 $p |- ( A. x A. y ( ( ph /\ ps ) -> ch ) <->
        A. x ( ph -> A. y ( ps -> ch ) ) ) $=
      ( wa wi wal impexp albii 19.21v bitri ) ABFCGZEHZABCGZEHGZDNAOGZEHPMQEABC
      IJAOEKLJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

    $( Theorem *11.63 in [WhiteheadRussell] p. 166. $)
    pm11.63 $p |- ( -. E. x E. y ph -> A. x A. y ( ph -> ps ) ) $=
      ( wex wn wal wi pm11.25 pm2.21 19.20i2 sylbi ) ADECEFAFZDGCGABHZDGCGACDIM
      NCDABJKL $.
      $( [25-May-2011] $) $( [24-May-2011] $)

    $( Theorem *11.7 in [WhiteheadRussell] p. 166. $)
    pm11.7 $p |- ( E. x E. y ( ph \/ ph ) <-> E. x E. y ph ) $=
      ( wo oridm 2exbii ) AADABCAEF $.
      $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ph y $.    $d ps y $.    $d ch x $.    $d th x $.
    $( Theorem *11.71 in [WhiteheadRussell] p. 166. $)
    pm11.71 $p |- ( ( E. x ph /\ E. y ch ) ->
        ( ( A. x ( ph -> ps ) /\ A. y ( ch -> th ) ) <->
        A. x A. y ( ( ph /\ ch ) -> ( ps /\ th ) ) ) ) $=
      ( wex wa wi wal ax-17 hbim aaan prth 19.20i2 sylbir hbex pm3.21 pm3.26
      imim2i syl9 19.22 19.42v 3imtr3g syl5 19.20d adantl pm3.2 pm3.27 19.41v
      ax-7 adantr jcad impbid2 ) AEGZCFGZHZABIZEJZCDIZFJZHZACHZBDHZIZFJZEJZVBUR
      UTHZFJEJVGURUTEFABFAFKZBFKLCDECEKZDEKLMVHVEEFABCDNOPUQVGUSVAUPVGUSIUOUPVF
      URECEFVJQUPAUPHZBDFGZHZIZURVFUPAVKVNBUPARVMBVKBVLSTUAVFVCFGVDFGVKVMVCVDFU
      BACFUCBDFUCUDUEUFUGUOVGVAIUPUOVEEJZFJVAVGUOVOUTFAFEVIQUOUOCHZBEGZDHZIZUTV
      OUOCVPVSDUOCUHVRDVPVQDUITUAVOVCEGVDEGVPVRVCVDEUBACEUJBDEUJUDUEUFVEEFUKUEU
      LUMUN $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

