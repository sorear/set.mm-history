$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Principia Mathematica * 11
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    2alanimi.1 $e |- ( ( ph /\ ps ) -> ch ) $.
    $( Removes two universal quantifiers from a statement. $)
    2alanimi $p |- ( ( A. x A. y ph /\ A. x A. y ps ) -> A. x A. y ch ) $=
      ( wal alanimi ) AEGBEGCEGDABCEFHH $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    2al2imi.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Removes two universal qunatifiers from a statement. $)
    2al2imi $p |- ( A. x A. y ph -> ( A. x A. y ps -> A. x A. y ch ) ) $=
      ( wal al2imi ) AEGBEGCEGDABCEFHH $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph x y z $.  $d w x z $.
    $( Theorem *11.07 in [WhiteheadRussell] p. 159. $)
    pm11.07 $p |- ( [ w / x ] [ y / z ] ph <-> [ y / x ] [ w / z ] ph ) $=
      ( cv wceq wa wex wsbc a9e pm3.2i 2th eeanv 3bitr4i anbi1i 19.41vv 2sb5 )
      BFZEFZGZDFZCFZGZHZAHDIBIZSUCGZUBTGZHZAHDIBIZADUCJBTJADTJBUCJUEDIBIZAHUIDI
      BIZAHUFUJUKULAUABIZUDDIZHZUGBIZUHDIZHZUKULUOURUMUNBEKDCKLUPUQBCKDEKLMUAUD
      BDNUGUHBDNOPUEABDQUIABDQOABDECRABDCERO $.
      $( [17-Jun-2011] $)
  $}

  $( Theorem *11.1 in [WhiteheadRussell] p. 159. $)
  stdpc4-2 $p |- ( A. x A. y ph -> [ z / x ] [ w / y ] ph ) $=
    ( wal cv wsbc stdpc4 alimi syl ) ACFZBFACEGHZBFMBDGHLMBACEIJMBDIK $.
    $( [17-Jun-2011] $) $( [24-May-2011] $)

  ${
    pm11.11.1 $e |- ph $.
    $( Theorem *11.11 in [WhiteheadRussell] p. 159. $)
    pm11.11 $p |- A. z A. w [ z / x ] [ w / y ] ph $=
      ( cv wsbc wal stdpc4-2 ax-gen mpg gen2 ) ACEGHBDGHZDEACINBABCDEJACFKLM $.
      $( [17-Jun-2011] $)
  $}

  ${
    $d ph x $.  $d ph y $.
    $( Theorem *11.12 in [WhiteheadRussell] p. 159. $)
    pm11.12 $p |- ( A. x A. y ( ph \/ ps ) -> ( ph \/ A. x A. y ps ) ) $=
      ( wo wal pm10.12 alimi syl ) ABEDFZCFABDFZEZCFAKCFEJLCABDGHAKCGI $.
      $( [17-Jun-2011] $)
  $}

  $( Theorem *11.21 in [WhiteheadRussell] p. 160. $)
  alrot3 $p |- ( A. x A. y A. z ph <-> A. y A. z A. x ph ) $=
    ( wal alcom albii bitri ) ADEZCEBEIBEZCEABEDEZCEIBCFJKCABDFGH $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.22 in [WhiteheadRussell] p. 160. $)
  2exnaln $p |- ( E. x E. y ph <-> -. A. x A. y -. ph ) $=
    ( wex wn wal df-ex alnex albii notbii bitr4i ) ACDZBDLEZBFZEAECFZBFZELBGPNO
    MBACHIJK $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.25 in [WhiteheadRussell] p. 160. $)
  2nexaln $p |- ( -. E. x E. y ph <-> A. x A. y -. ph ) $=
    ( wn wal wex 2exnaln bicomi con1bii ) ADCEBEZACFBFZKJDABCGHI $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ps x $.  $d ps y $.
    $( Compare theorem *11.3 in [WhiteheadRussell] p. 161.  Special case of
       theorem 19.21 of [Margaris] p. 90 with 2 quantifiers. $)
    19.21vv $p |- ( A. x A. y ( ps -> ph ) <-> ( ps -> A. x A. y ph ) ) $=
      ( wi wal 19.21v albii bitri ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  $( Theorem *11.32 in [WhiteheadRussell] p. 162.  Theorem 19.20 of [Margaris]
     p. 90 with 2 quantifiers. $)
  2alim $p |- ( A. x A. y ( ph -> ps ) ->
        ( A. x A. y ph -> A. x A. y ps ) ) $=
    ( wi id 2al2imi ) ABEZABCDHFG $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.33 in [WhiteheadRussell] p. 162.  Theorem 19.15 of [Margaris]
     p. 90 with 2 quantifiers. $)
  2albi $p |- ( A. x A. y ( ph <-> ps ) ->
        ( A. x A. y ph <-> A. x A. y ps ) ) $=
    ( wb wal hba1 albi a4s albid ) ABEDFZCFADFZBDFZCKCGKLMECABDHIJ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.34 in [WhiteheadRussell] p. 162.  Theorem 19.22 of [Margaris]
     p. 90 with 2 quantifiers. $)
  2exim $p |- ( A. x A. y ( ph -> ps ) ->
        ( E. x E. y ph -> E. x E. y ps ) ) $=
    ( wi wal wex exim alimi syl ) ABEDFZCFADGZBDGZEZCFLCGMCGEKNCABDHILMCHJ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.341 in [WhiteheadRussell] p. 162.  Theorem 19.18 of [Margaris]
     p. 90 with 2 quantifiers. $)
  2exbi $p |- ( A. x A. y ( ph <-> ps ) ->
        ( E. x E. y ph <-> E. x E. y ps ) ) $=
    ( wb wal wex exbi alimi syl ) ABEDFZCFADGZBDGZEZCFLCGMCGEKNCABDHILMCHJ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.36 in [WhiteheadRussell] p. 162. $)
  a4sbce-2 $p |- ( [ z / x ] [ w / y ] ph -> E. x E. y ph ) $=
    ( cv wsbc wex a4sbe eximi syl ) ACEFGZBDFGLBHACHZBHLBDILMBACEIJK $.
    $( [17-Jun-2011] $) $( [24-May-2011] $)

  $( Theorem *11.42 in [WhiteheadRussell] p. 163.  Theorem 19.40 of [Margaris]
     p. 90 with 2 quantifiers. $)
  19.40-2 $p |- ( E. x E. y ( ph /\ ps ) ->
        ( E. x E. y ph /\ E. x E. y ps ) ) $=
    ( wa wex 19.40 eximi syl ) ABEDFZCFADFZBDFZEZCFKCFLCFEJMCABDGHKLCGI $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.421 in [WhiteheadRussell] p. 163.  Theorem 19.33 of [Margaris]
     p. 90 with 2 quantifiers. $)
  19.33-2 $p |- ( ( A. x A. y ph \/ A. x A. y ps ) ->
        A. x A. y ( ph \/ ps ) ) $=
    ( wal wo orc 2alimi olc jaoi ) ADECEABFZDECEBDECEAKCDABGHBKCDBAIHJ $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ps x $.  $d ps y $.
    $( Theorem *11.43 in [WhiteheadRussell] p. 163.  Theorem 19.36 of
       [Margaris] p. 90 with 2 quantifiers. $)
    19.36vv $p |- ( E. x E. y ( ph -> ps ) <-> ( A. x A. y ph -> ps ) ) $=
      ( wi wex wal 19.36v exbii bitri ) ABEDFZCFADGZBEZCFLCGBEKMCABDHILBCHJ $.
      $( [25-May-2011] $)
  $}

  ${
    $d ps x $.  $d ps y $.
    $( Theorem *11.44 in [WhiteheadRussell] p. 163.  Theorem 19.31 of
       [Margaris] p. 90 with 2 quantifiers. $)
    19.31vv $p |- ( A. x A. y ( ph \/ ps ) <-> ( A. x A. y ph \/ ps ) ) $=
      ( wo wal ax-17 19.31 albii bitri ) ABEDFZCFADFZBEZCFLCFBEKMCABDBDGHILBCBC
      GHJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ps x $.  $d ps y $.
    $( Theorem *11.46 in [WhiteheadRussell] p. 164.  Theorem 19.37 of
       [Margaris] p. 90 with 2 quantifiers. $)
    19.37vv $p |- ( E. x E. y ( ps -> ph ) <-> ( ps -> E. x E. y ph ) ) $=
      ( wi wex 19.37v exbii bitri ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ps x $.  $d ps y $.
    $( Theorem *11.47 in [WhiteheadRussell] p. 164.  Theorem 19.28 of
       [Margaris] p. 90 with 2 quantifiers. $)
    19.28vv $p |- ( A. x A. y ( ps /\ ph ) <-> ( ps /\ A. x A. y ph ) ) $=
      ( wa wal 19.28v albii bitri ) BAEDFZCFBADFZEZCFBKCFEJLCBADGHBKCGI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  $( Part of theorem *11.5 in [WhiteheadRussell] p. 164. $)
  2nalexn $p |- ( -. A. x A. y ph <-> E. x E. y -. ph ) $=
    ( wal wn wex alex albii notbii df-ex bitr4i ) ACDZBDZEAECFZEZBDZENBFMPLOBAC
    GHINBJK $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.51 in [WhiteheadRussell] p. 164. $)
  2exnexn $p |- ( E. x A. y ph <-> -. A. x E. y -. ph ) $=
    ( wal wex wn df-ex exnal albii notbii bitr4i ) ACDZBELFZBDZFAFCEZBDZFLBGPNO
    MBACHIJK $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.52 in [WhiteheadRussell] p. 164. $)
  pm11.52 $p |- ( E. x E. y ( ph /\ ps ) <->
        -. A. x A. y ( ph -> -. ps ) ) $=
    ( wa wex wn wi wal df-an 2exbii 2nalexn bitr4i ) ABEZDFCFABGHZGZDFCFODICIGN
    PCDABJKOCDLM $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.521 in [WhiteheadRussell] p. 164. $)
  2exanali $p |- ( -. E. x E. y ( ph /\ -. ps ) <->
        A. x A. y ( ph -> ps ) ) $=
    ( wn wa wex wi wal annim 2exbii notbii 2nalexn con1bii bitri ) ABEFZDGCGZEA
    BHZEZDGCGZERDICIZQTPSCDABJKLUATRCDMNO $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ph y $.  $d ps x $.
    $( Theorem *11.53 in [WhiteheadRussell] p. 164. $)
    pm11.53 $p |- ( A. x A. y ( ph -> ps ) <-> ( E. x ph -> A. y ps ) ) $=
      ( wi wal wex 19.21v albii ax-17 hbal 19.23 bitri ) ABEDFZCFABDFZEZCFACGOE
      NPCABDHIAOCBCDBCJKLM $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.  $d ps x $.
    $( Theorem *11.56 in [WhiteheadRussell] p. 165.  Special case of
       ~ aaan . $)
    aaanv $p |- ( ( A. x ph /\ A. y ps ) <-> A. x A. y ( ph /\ ps ) ) $=
      ( wa wal ax-17 aaan bicomi ) ABEDFCFACFBDFEABCDADGBCGHI $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.57 in [WhiteheadRussell] p. 165. $)
    pm11.57 $p |- ( A. x ph <-> A. x A. y ( ph /\ [ y / x ] ph ) ) $=
      ( wal cv wsbc wa ax-17 hbal ax-4 stdpc4 jca 19.21ai a5i simpl a4s alimi
      impbii ) ABDZAABCEFZGZCDZBDAUBBSUACACBACHISATABJABCKLMNUBABUAACATOPQR $.
      $( [8-Jun-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.58 in [WhiteheadRussell] p. 165. $)
    pm11.58 $p |- ( E. x ph <-> E. x E. y ( ph /\ [ y / x ] ph ) ) $=
      ( cv wsbc wa wex id 19.8a ax-17 sb8e biimpi syl jca simpl impbii 19.42v
      bitr4i exbii ) AAABCDEZFCGZBAATCGZFZUAAUCAAUBAHAABGZUBABIUDUBABCACJKLMNAU
      BOPATCQRS $.
      $( [8-Jun-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.  $d ps y $.
    $( Theorem *11.59 in [WhiteheadRussell] p. 165. $)
    pm11.59 $p |- ( A. x ( ph -> ps ) ->
        A. y A. x ( ( ph /\ [ y / x ] ph ) -> ( ps /\ [ y / x ] ps ) ) ) $=
      ( wi wal cv wsbc wa ax-17 hbal ax-4 a4sbim anim12d a5i 19.21ai ) ABEZCFZA
      ACDGZHZIBBCSHZIEZCFDQDCQDJKQUBCRABTUAQCLABCDMNOP $.
      $( [8-Jun-2011] $) $( [25-May-2011] $)
  $}

  ${
    $d ps x $.  $d ch y $.
    $( Theorem *11.6 in [WhiteheadRussell] p. 165. $)
    pm11.6 $p |- ( E. x ( E. y ( ph /\ ps ) /\ ch ) <->
        E. y ( E. x ( ph /\ ch ) /\ ps ) ) $=
      ( wa wex excom an32 2exbii bitri 19.41v exbii 3bitr3i ) ABFZCFZEGZDGZACFZ
      BFZDGZEGZOEGCFZDGSDGBFZEGRPDGEGUBPDEHPTEDABCIJKQUCDOCELMUAUDESBDLMN $.
      $( [25-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.61 in [WhiteheadRussell] p. 166. $)
    pm11.61 $p |- ( E. y A. x ( ph -> ps ) -> A. x ( ph -> E. y ps ) ) $=
      ( wi wal wex 19.12 19.37v biimpi alimi syl ) ABEZCFDGMDGZCFABDGEZCFMDCHNO
      CNOABDIJKL $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  ${
    $d ph y $.
    $( Theorem *11.62 in [WhiteheadRussell] p. 166.  Importation combined with
       the rearrangement with quantifiers. $)
    pm11.62 $p |- ( A. x A. y ( ( ph /\ ps ) -> ch ) <->
        A. x ( ph -> A. y ( ps -> ch ) ) ) $=
      ( wa wi wal impexp albii 19.21v bitri ) ABFCGZEHZABCGZEHGZDNAOGZEHPMQEABC
      IJAOEKLJ $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

  $( Theorem *11.63 in [WhiteheadRussell] p. 166. $)
  pm11.63 $p |- ( -. E. x E. y ph -> A. x A. y ( ph -> ps ) ) $=
    ( wex wn wal wi 2nexaln pm2.21 2alimi sylbi ) ADECEFAFZDGCGABHZDGCGACDIMNCD
    ABJKL $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  $( Theorem *11.7 in [WhiteheadRussell] p. 166. $)
  pm11.7 $p |- ( E. x E. y ( ph \/ ph ) <-> E. x E. y ph ) $=
    ( wo oridm 2exbii ) AADABCAEF $.
    $( [25-May-2011] $) $( [24-May-2011] $)

  ${
    $d ph y $.  $d ps y $.  $d ch x $.  $d th x $.
    $( Theorem *11.71 in [WhiteheadRussell] p. 166. $)
    pm11.71 $p |- ( ( E. x ph /\ E. y ch ) ->
        ( ( A. x ( ph -> ps ) /\ A. y ( ch -> th ) ) <->
        A. x A. y ( ( ph /\ ch ) -> ( ps /\ th ) ) ) ) $=
      ( wex wa wi wal ax-17 hbim aaan prth 2alimi sylbir hbex pm3.21 simpl
      imim2i syl9 exim 19.42v 3imtr3g syl5 alimd adantl pm3.2 simpr 19.41v
      ax-7 adantr jcad impbid2 ) AEGZCFGZHZABIZEJZCDIZFJZHZACHZBDHZIZFJZEJZVBUR
      UTHZFJEJVGURUTEFABFAFKZBFKLCDECEKZDEKLMVHVEEFABCDNOPUQVGUSVAUPVGUSIUOUPVF
      URECEFVJQUPAUPHZBDFGZHZIZURVFUPAVKVNBUPARVMBVKBVLSTUAVFVCFGVDFGVKVMVCVDFU
      BACFUCBDFUCUDUEUFUGUOVGVAIUPUOVEEJZFJVAVGUOVOUTFAFEVIQUOUOCHZBEGZDHZIZUTV
      OUOCVPVSDUOCUHVRDVPVQDUITUAVOVCEGVDEGVPVRVCVDEUBACEUJBDEUJUDUEUFVEEFUKUEU
      LUMUN $.
      $( [25-May-2011] $) $( [24-May-2011] $)
  $}

