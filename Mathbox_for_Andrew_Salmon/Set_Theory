$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( Any set that contains one element less than the universe is not equal to
       it. $)
    elnev $p |- ( A e. _V <-> { x | -. x = A } =/= _V ) $=
      ( cv wceq wex wn cab cvv wcel wne wal df-v eqeq2i wb eq2ab equid tbt
      albii bitr4i alnex 3bitri con2bii isset df-ne 3bitr4i ) ACZBDZAEZUGFZAGZH
      DZFBHIUJHJUKUHUKUJUFUFDZAGZDZUIAKZUHFHUMUJALMUNUIULNZAKUOUIULAOUIUPAULUIA
      PQRSUGATUAUBABUCUJHUDUE $.
      $( [16-Jun-2011] $)
  $}

  ${
    $d A y $.  $d x y $.
    isseta.1 $e |- ( y e. A -> A. x y e. A ) $.
    isseta.2 $e |- ( x = A -> A. y x = A ) $.
    $( A version of isset that does not require x and A to be distinct. $)
    isseta $p |- ( A e. _V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wex isset hbeleq eqeq1 cbvex bitri ) CFGBHZCIZBJAHZCIZ
      AJBCKQSBAABCDLEPRCMNO $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x y $.  $d y A $.
    cla4gft.1 $e |- ( ps -> A. x ps ) $.
    cla4gft.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( A closed version of cla4gf. $)
    cla4gft $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                      ( A. x ph -> ps ) ) ) $=
      ( cv wceq wb wi wal cvv wcel wex hba1 bi1 imim2i a4s eximd ax-17 isseta
      syl5ib 19.36 syl6ib elisset syl5 ) CIEJZABKZLZCMZENOZACMBLZEFOULUMABLZCPZ
      UNULUICPUPUMULUIUOCUKCQUKUIUOLCUJUOUIABRSTUACDEHUIDUBUCUDABCGUEUFEFUGUH
      $.
      $( [6-Jun-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d A z $.  $d x z $.
    rcla4t.1 $e |- ( ps -> A. x ps ) $.
    $( A closed version of rcla4. $)
    rcla4t $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                   ( A. x e. B ph -> ps ) ) ) $=
      ( vz cv wceq wb wi wal wcel wral wa eleq1 adantr pm3.27 imbi12d ex a2i
      alimi ax-17 hbim cla4gft syl df-ral syl7ib com34 pm2.43d ) CHZDIZABJZKZCL
      ZDEMZACENZBKUOUPUQUPBUOUPUKEMZAKZCLZUPBKZUQUOULUSVAJZKZCLUPUTVAKKUNVCCULU
      MVBULUMVBULUMOURUPABULURUPJUMUKDEPQULUMRSTUAUBUSVACGDEUPBCUPCUCFUDGHDMCUC
      UEUFACEUGUHUIUJ $.
      $( [6-Jun-2011] $)
  $}

  ${
    $( Distribute proper substitution through negated membership. $)
    sbcnel12g $p |- ( A e. D -> ( [ A / x ] B e/ C <-> [_ A / x ]_ B e/
                      [_ A / x ]_ C ) ) $=
      ( wcel wnel wsbc wn csb df-nel sbcbii sbcng sbcel12g notbid syl6bbr
      3bitrd ) BEFZCDGZABHCDFZIZABHTABHZIZABCJZABDJZGZSUAABECDKLTABEMRUCUDUEFZI
      UFRUBUGABCDENOUDUEKPQ $.
      $( [28-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $( Distribute proper substitution through an inequality. $)
    sbcne12g $p |- ( A e. D -> ( [ A / x ] B =/= C <-> [_ A / x ]_ B =/=
                     [_ A / x ]_ C ) ) $=
      ( wcel wne wsbc csb wn wceq nne sbcbii sbcng sbceqdig syl6bbr 3bitr3d
      con4bid ) BEFZCDGZABHZABCIZABDIZGZSTJZABHCDKZABHZUAJUDJZUEUFABECDLMTABENS
      UGUBUCKUHABCDEOUBUCLPQR $.
      $( [28-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $( A version of Russell's paradox which is proven using proper
       substitution. $)
    rusbcALT $p |- { x | x e/ x } e/ _V $=
      ( cv wnel cab cvv wcel wn wb pm5.19 wsbc csb elabsg sbcnel12g csbvarg
      eleq12d notbid df-nel syl5bb 3bitrd mto mpbir ) ABZUBCZADZECUDEFZGUEUDUDF
      ZUFGZHUFIUEUFUCAUDJAUDUBKZUHCZUGUCAUDELAUDUBUBEMUEUHUHFZGUGUIUEUJUFUEUHUD
      UHUDAUDENZUKOPUHUHQRSTUDEQUA $.
      $( [29-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $d x v z $.  $d y v z $.  $d A w v z $.  $d B w v z $.  $d ph v z $.
    $d ps v z $.
    cbvralcsf.1 $e |- ( x = y -> A = B ) $.
    cbvralcsf.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbvralcsf.3 $e |- ( w e. A -> A. y w e. A ) $.
    cbvralcsf.4 $e |- ( w e. B -> A. x w e. B ) $.
    cbvralcsf.5 $e |- ( ph -> A. y ph ) $.
    cbvralcsf.6 $e |- ( ps -> A. x ps ) $.
    $( A more general version of ~ cbvralf that doesn't require A and B to be
       distinct from x or y.  Changes bound variables using implicit
       substitution. $)
    cbvralcsf $p |- ( A. x e. A ph <-> A. y e. B ps ) $=
      ( vz vv cv wcel wi wal wral csb wsbc ax-17 visset hbcsb1 hbel hbs1 hbim
      wceq id csbeq1a eleq12d sbequ12 imbi12d cbval cvv hblem hbcsbg ax-mp
      hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ
      syl6bb bitri df-ral 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVRNPZCWCFUA
      ZQZACWCUBZRZNSWBVQWGCNVQNUCWEWFCCOOWCWDOPZWCQZCUCZCOWCFNUDZWJUEUFACNUGUHV
      OWCUIZVPWEAWFWLVOWCFWDWLUJCWCFUKULACNUMUNUOWGWANDWEWFDDOOWCWDWIDUCZWCUPQW
      HWDQZWNDSRWKDCOWCFUPWMDEOFJUQURUSUFACNDLUTUHWANUCWCVSUIZWEVTWFBWOWCVSWDGW
      OUJWOWDCVSFUAZGCWCVSFVAWPWHFQZCVSUBZOVBGCOVSFVCWROGWRWHGQZWQWSCDCEOGKUQVO
      VSUIFGWHHVDVEVFVGVHVIULWOWFACVSUBBANDCVJABCDMIVEVKUNUOVLACFVMBDGVMVN $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrexf that has no distinct variable
       restrictions.  Changes bound variables using implicit substitution. $)
    cbvrexcsf $p |- ( E. x e. A ph <-> E. y e. B ps ) $=
      ( vz vv cv wcel wa wex wrex csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbvex cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq
      sbequ syl6bb bitri df-rex 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVTNPZ
      CWEFUAZQZACWEUBZRZNSWDVSWICNVSNUCWGWHCCOOWEWFOPZWEQZCUCZCOWEFNUDZWLUEUFAC
      NUGUHVQWEUIZVRWGAWHWNVQWEFWFWNUJCWEFUKULACNUMUNUOWIWCNDWGWHDDOOWEWFWKDUCZ
      WEUPQWJWFQZWPDUQURWMDCOWEFUPWODEOFJUSUTVAUFACNDLVBUHWCNUCWEWAUIZWGWBWHBWQ
      WEWAWFGWQUJWQWFCWAFUAZGCWEWAFVCWRWJFQZCWAUBZOVDGCOWAFVEWTOGWTWJGQZWSXACDC
      EOGKUSVQWAUIFGWJHVFVGVHVIVJVKULWQWHACWAUBBANDCVLABCDMIVGVMUNUOVNACFVOBDGV
      OVP $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvreuv that has no distinct variable
       rextrictions.  Changes bound variables using implicit substitution. $)
    cbvreucsf $p |- ( E! x e. A ph <-> E! y e. B ps ) $=
      ( vz vv cv wcel wa weu wreu csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbveu cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq
      sbequ syl6bb bitri df-reu 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVTNPZ
      CWEFUAZQZACWEUBZRZNSWDVSWICNVSNUCWGWHCCOOWEWFOPZWEQZCUCZCOWEFNUDZWLUEUFAC
      NUGUHVQWEUIZVRWGAWHWNVQWEFWFWNUJCWEFUKULACNUMUNUOWIWCNDWGWHDDOOWEWFWKDUCZ
      WEUPQWJWFQZWPDUQURWMDCOWEFUPWODEOFJUSUTVAUFACNDLVBUHWCNUCWEWAUIZWGWBWHBWQ
      WEWAWFGWQUJWQWFCWAFUAZGCWEWAFVCWRWJFQZCWAUBZOVDGCOWAFVEWTOGWTWJGQZWSXACDC
      EOGKUSVQWAUIFGWJHVFVGVHVIVJVKULWQWHACWAUBBANDCVLABCDMIVGVMUNUOVNACFVOBDGV
      OVP $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrab with no distinct variable
       restrictions. $)
    cbvrabcsf $p |- { x e. A | ph } = { y e. B | ps } $=
      ( vz vv cv wcel wa cab crab csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbvab cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ
      syl6bb eqtri df-rab 3eqtr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVSNPZCWDFUA
      ZQZACWDUBZRZNSWCVRWHCNVRNUCWFWGCCOOWDWEOPZWDQZCUCZCOWDFNUDZWKUEUFACNUGUHV
      PWDUIZVQWFAWGWMVPWDFWEWMUJCWDFUKULACNUMUNUOWHWBNDWFWGDDOOWDWEWJDUCZWDUPQW
      IWEQZWODUQURWLDCOWDFUPWNDEOFJUSUTVAUFACNDLVBUHWBNUCWDVTUIZWFWAWGBWPWDVTWE
      GWPUJWPWECVTFUAZGCWDVTFVCWQWIFQZCVTUBZOSGCOVTFVDWSOGWSWIGQZWRWTCDCEOGKUSV
      PVTUIFGWIHVEVFVGVHVIVJULWPWGACVTUBBANDCVKABCDMIVFVLUNUOVMACFVNBDGVNVO $.
      $( [13-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( Equivalence between two ways of saying "is a member of the complement of
       ` A ` ." $)
    compel $p |- ( x e. ( _V \ A ) <-> -. x e. A ) $=
      ( cv cvv cdif wcel wn wa eldif visset biantrur bitr4i ) ACZDBEFMDFZMBFGZH
      OMDBINOAJKL $.
      $( [15-Jul-2011] $)

    $( Equality between two ways of saying "the complement of ` A ` ." $)
    compeq $p |- ( _V \ A ) = { x | -. x e. A } $=
      ( cv wcel wn cvv cdif compel abbi2i ) ACBDEAFBGABHI $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d A x y $.
    $( The complement of ` A ` is not equal to ` A ` . $)
    compne $p |- ( _V \ A ) =/= A $=
      ( vx vy cvv cdif wne wceq wn cv wcel cab wb wal wsbc pm5.19 ax-4 df-clab
      bibi12i sbn bibi1i bicom 3bitri sylib mto dfcleq mtbir compeq eqcomi
      abid2 eqeq12i mtbi df-ne mpbir ) DAEZAFUNAGZHBIAJZHZBKZUPBKZGZUOUTCIZURJZ
      VAUSJZLZCMZVEUPBVANZVFHZLZVFOVEVDVHVDCPVDUQBVANZVFLVGVFLVHVBVIVCVFUQCBQUP
      CBQRVIVGVFUPBCSTVGVFUAUBUCUDCURUSUEUFURUNUSAUNURBAUGUHBAUIUJUKUNAULUM $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d x ph $.  $d x z $.
    $( Two ways of saying "the complement of a class abstraction". $)
    compab $p |- ( _V \ { z | ph } ) = { z | -. ph } $=
      ( vx cvv cab cdif cv wcel wn compeq hbab1 hbn ax-17 wceq wsbc sbequ12r
      df-clab syl5bb notbid cbvab eqtri ) DABEZFCGZUBHZIZCEAIZBECUBJUEUFCBUDBAB
      CKLUFCMUCBGNZUDAUGABUCOAUDACBPACBQRSTUA $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Contrpositive law for subsets. $)
    conss34 $p |- ( A C_ B <-> ( _V \ B ) C_ ( _V \ A ) ) $=
      ( vx cv wcel wi wal cvv cdif wss wn con34b compel imbi12i bitr4i albii
      dfss2 3bitr4i ) CDZAEZSBEZFZCGSHBIZEZSHAIZEZFZCGABJUCUEJUBUGCUBUAKZTKZFUG
      TUALUDUHUFUICBMCAMNOPCABQCUCUEQR $.
      $( [15-Jul-2011] $)

    $( Contrapositive law for subsets. $)
    conss2 $p |- ( A C_ ( _V \ B ) <-> B C_ ( _V \ A ) ) $=
      ( cvv wss cdif wb ssv ssconb mp2an ) ACDBCDACBEDBCAEDFAGBGABCHI $.
      $( [15-Jul-2011] $)

    $( Contrapositive law for subsets. $)
    conss1 $p |- ( ( _V \ A ) C_ B <-> ( _V \ B ) C_ A ) $=
      ( vx cv cvv cdif wcel wi wal wss wn con1b compel imbi1i 3bitr4i albii
      dfss2 ) CDZEAFZGZRBGZHZCIREBFZGZRAGZHZCISBJUCAJUBUFCUEKZUAHUAKZUEHUBUFUEU
      ALTUGUACAMNUDUHUECBMNOPCSBQCUCAQO $.
      $( [15-Jul-2011] $)
  $}

  ${
    ralbidar.1 $e |- ( ph -> A. x e. A ph ) $.
    ralbidar.2 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( More general form of ~ ralbida . $)
    ralbidar $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral wb ex ralimi syl df-ral sylib pm2.43 pm5.74d alimi
      albi 3syl 3bitr4g ) ADHEIZBJZDKZUECJZDKZBDELCDELAUEUEBCMZJZJZDKZUFUHMZDKU
      GUIMAUKDELZUMAADELUOFAUKDEAUEUJGNOPUKDEQRULUNDULUEBCUEUJSTUAUFUHDUBUCBDEQ
      CDEQUD $.
      $( [25-Jul-2011] $)

    $( More general form of ~ rexbida . $)
    rexbidar $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( cv wcel wa wex wrex wb wi wal wral ex ralimi syl df-ral sylib pm2.43
      pm5.32d alimi exbi 3syl df-rex 3bitr4g ) ADHEIZBJZDKZUICJZDKZBDELCDELAUIU
      IBCMZNZNZDOZUJULMZDOUKUMMAUODEPZUQAADEPUSFAUODEAUIUNGQRSUODETUAUPURDUPUIB
      CUIUNUBUCUDUJULDUEUFBDEUGCDEUGUH $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d ph w $.  $d w x $.  $d w y $.  $d w z $.
    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction. $)
    dropab1 $p |- ( A. x x = y -> { <. x , z >. | ph } =
        { <. y , z >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq1 a4s eqeq2d anbi1d drex2
      drex1 abbidv df-opab 3eqtr4g ) BFZCFZGZBHZEFZUCDFZIZGZAJZDKZBKZELUGUDUHIZ
      GZAJZDKZCKZELABDMACDMUFUMUREULUQBCUKUPBCDUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOP
      QRSTABDEUAACDEUAUB $.
      $( [25-Jul-2011] $)

    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction. $)
    dropab2 $p |- ( A. x x = y -> { <. z , x >. | ph } =
        { <. z , y >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq2 a4s eqeq2d anbi1d drex1
      drex2 abbidv df-opab 3eqtr4g ) BFZCFZGZBHZEFZDFZUCIZGZAJZBKZDKZELUGUHUDIZ
      GZAJZCKZDKZELADBMADCMUFUMUREULUQBCDUKUPBCUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOP
      QRSTADBEUAADCEUAUB $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( If the identity relation partially orders any class, then that class is
       the null class. $)
    ipo0 $p |- ( _I Po A <-> A = (/) ) $=
      ( vx cid wpo c0 wceq wne cv wcel wex wn n0 wbr weq equid visset ideq
      mpbir poirr ex mt2i con2i 19.23aiv sylbi necon4ai po0 poeq2 mpbiri
      impbii ) ACDZAEFZUJAEAEGBHZAIZBJUJKZBALUMUNBUJUMUJUMULULCMZUOBBNBOULULBPQ
      RUJUMUOKAULCSTUAUBUCUDUEUKUJECDCUFAECUGUHUI $.
      $( [25-Jul-2011] $)

    $( A class that is founded by the identity relation is null. $)
    ifr0 $p |- ( _I Fr A <-> A = (/) ) $=
      ( vx cid wfr c0 wceq wne cv wcel wex wn n0 wbr equid visset ideq mpbir
      frirr ex mt2i con2i 19.23aiv sylbi necon4ai fr0 freq2 mpbiri impbii ) ACD
      ZAEFZUIAEAEGBHZAIZBJUIKZBALULUMBUIULUIULUKUKCMZUNUKUKFBNUKUKBOPQUIULUNKBA
      CRSTUAUBUCUDUJUIECDCUEAECUFUGUH $.
      $( [25-Jul-2011] $)

    $( ~ ordelpss with an antecedent removed. $)
    ordpss $p |- ( Ord B -> ( A e. B -> A C. B ) ) $=
      ( word wcel wa wpss ordelord ex ancrd wb ordelpss ancoms biimpd expimpd
      syld ) BCZABDZACZQEABFZPQRPQRBAGHIPRQSPREQSRPQSJABKLMNO $.
      $( [25-Jul-2011] $)
  $}

  ${
    $d y x F $.  $d y x A $.
    fviota.1 $e |- A e. _V $.
    $( Alternate definition of the value of a function.  The value of a
       function expressed using ` iota ` . $)
    fviota $p |- ( F ` A ) = ( iota x A F x ) $=
      ( vy cfv cv wbr wceq wb wal cab cuni cio fv2 dfiota2 eqtr4i ) BCFBAGZCHZR
      EGIJAKELMSANEABCDOSAEPQ $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    fvsb.1 $e |- A e. _V $.
    $( Explicit substitution of a value of a function into a wff. $)
    fvsb $p |- ( E! y A F y -> ( [ ( F ` A ) / x ] ph <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cv wbr weu cio wsbc wceq wb wal wa wex cfv iotasbc fviota dfsbcq ax-mp
      syl5bb ) DCGZEHZCIABUDCJZKZUDUCBGLMCNAOBPABDEQZKZUDACBRUGUELUHUFMCDEFSABU
      GUETUAUB $.
      $( [1-Aug-2011] $)

    fveqsb.2 $e |- ( x = ( F ` A ) -> ( ph <-> ps ) ) $.
    fveqsb.3 $e |- ( ps -> A. x ps ) $.
    $( Implicit substitution of a value of a function into a wff. $)
    fveqsb $p |- ( E! y A F y -> ( ps <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cv wbr weu cfv wsbc wceq wb wal wa wex fvsb cvv wcel fvex wi a1i
      sbciegf ax-mp syl5bbr ) EDJZFKZDLACEFMZNZUJUICJOPDQARCSBACDEFGTUKUAUBZULB
      PEFUCABCUKUABBCQUDUMIUEHUFUGUH $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d z w x $.  $d z w y $.  $d z w ph $.  $d z w ps $.
    cbviotaf.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbviotaf.2 $e |- ( ph -> A. y ph ) $.
    cbviotaf.3 $e |- ( ps -> A. x ps ) $.
    $( Changes bound variables in a description. $)
    cbviotaf $p |- ( iota x ph ) = ( iota y ps ) $=
      ( vw vz cv wceq wb wal cab cuni cio wsbc ax-17 hbs1 hbbi sbequ12 equequ1
      bibi12d cbval hbsb sbequ sbie syl6bb bitri abbii unieqi dfiota2 3eqtr4i
      ) ACJZHJZKZLZCMZHNZOBDJZUOKZLZDMZHNZOACPBDPUSVDURVCHURACIJZQZVEUOKZLZIMVC
      UQVHCIUQIRVFVGCACISVGCRTUNVEKAVFUPVGACIUACIHUBUCUDVHVBIDVFVGDACIDFUEVGDRT
      VBIRVEUTKZVFBVGVAVIVFACUTQBAIDCUFABCDGEUGUHIDHUBUCUDUIUJUKACHULBDHULUM $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d ph y $.  $d ps x $.
    cbviota.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Changes bound variables in a description. $)
    cbviota $p |- ( iota x ph ) = ( iota y ps ) $=
      ( ax-17 cbviotaf ) ABCDEADFBCFG $.
      $( [1-Aug-2011] $)
  $}
  ${
    $d x y z A $.  $d x y B $.
    $( 'Less than' implies not equal for extended reals. $)
    xrltneNEW $p |- ( ( A e. RR* /\ A < B ) -> A =/= B ) $=
      ( cxr wcel clt wbr wne wceq wn breq2 notbid xrltnr syl5cbi necon2ad imp 
      ) ACDZABEFZABGPQABABHZAAEFZIQIPRSQABAEJKALMNO $.
      $( [11-Nov-2011] $) $( [11-Nov-2011] $)

    $( A Cartesian product exists iff its converse does.  Corrolary 6.9(1) in
       [TakeutiZaring] p. 26. $)
    xpexb $p |- ( ( A X. B ) e. _V <-> ( B X. A ) e. _V ) $=
      ( cxp cvv wcel ccnv cnvexg cnvxp syl5eqelr impbii ) ABCZDEZBACZDEZLKFDMKD
      GABHINMFDKMDGBAHIJ $.
      $( [13-Nov-2011] $) $( [13-Nov-2011] $)
    
    $( A condition where the converse of ~ xpex holds as well.  Corrolary
       6.9(2) in [TakeutiZaring] p. 26. $)
    xpexcnv $p |- ( ( B =/= (/) /\ ( A X. B ) e. _V ) -> A e. _V ) $=
      ( c0 wne cxp cvv wcel cdm dmxp eleq1d dmexg syl5bi imp ) BCDZABEZFGZAFGZN
      OHZFGQPNRAFABIJOFKLM $.
      $( [13-Nov-2011] $) $( [13-Nov-2011] $)

    $( An element of a transitive set is a proper subset of it.  Theorem 7.2
       in [TakeutiZaring] p. 35.  Unlike ~ tz7.2 , ~ ax-reg is required for
       its proof. $)
    trelpss $p |- ( ( Tr A /\ B e. A ) -> B C. A ) $=
      ( wtr wcel wa wss wne wpss cep wfr zfregfr tz7.2 mp3an2 df-pss sylibr ) 
      ACZBADZEBAFBAGEZBAHPAIJQRAKABLMBANO $.
      $( [13-Nov-2011] $) $( [13-Nov-2011] $)

    ltfrn.1 $e |- A e. ZZ $.
    $( ` < ` is a well-founded relation on any sequence of upper integers. $)
    ltfrn $p |- < We { x e. ZZ | A <_ x } $=
      ( vz vy com cep wwe cv cle wbr cz crab clt con0 wss omsson epweon wess 
      mp2 c1 caddc co wceq copab crdg cres wiso wb eqid om2uzisoi isowe ax-mp 
      mpbi ) FGHZBAIJKALMZNHZFOPOGHUOQRFOGSTFUPGNDIEIUAUBUCUDEDUEBUFFUGZUHUOUQU
      IEDABURCURUJUKFUPGNURULUMUN $.
      $( [13-Nov-2011] $) $( [13-Nov-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Any non-empty transitive class contains its intersection.  Exercise 2
       in [TakeutiZaring] p. 44. $)
    trint0 $p |- ( ( Tr A /\ A =/= (/) ) -> |^| A C_ A ) $=
      ( vx c0 wne wtr cint wss cv wcel wex wi n0 trss com12 intss1 sstr2 syl 
      syld 19.23aiv sylbi impcom ) ACDZAEZAFZAGZUBBHZAIZBJUCUEKZBALUGUHBUGUCUFA
      GZUEUCUGUIAUFMNUGUDUFGUIUEKUFAOUDUFAPQRSTUA $.
      $( [14-Nov-2011] $) $( [14-Nov-2011] $)

    $( If ` B ` is smaller than ` A ` , then it equals the intersection of the
       difference.  Exercise 11 in [TakeutiZaring] p. 44. $)
    ordintdif $p |- ( ( Ord A /\ Ord B /\ ( A \ B ) =/= (/) ) -> B = |^| ( A \
       B ) ) $=
      ( vx word wss wn cdif cint wceq c0 wne w3a cv wcel crab wa ordtri1 
      con2bid wb ancoms ordelord sylan an1rs bicomd rabbidva inteqd intmin 
      sylan9eq ex sylbird 3impia dfdif2 inteqi syl5req ssdif0 necon3bbii 
      syl3an3br ) ADZBDZABEZFZBABGZHZIVBJKURUSVALCMZBNFZCAOZHZBVCURUSVAVGBIZURU
      SPZVABANZVHVIUTVJABQRVIVJVHVIVJVGBVDEZCAOZHBVIVFVLVIVEVKCAVIVDANZPVKVEURV
      MUSVKVESZVDDZUSVNURVMPUSVOVNBVDQTAVDUAUBUCUDUEUFCBAUGUHUIUJUKVBVFCABULUMU
      NUTVBJABUOUPUQ $.
      $( [14-Nov-2011] $) $( [14-Nov-2011] $)
  $}

    $c Smo $.

  ${
    $( Introduce the strictly monotone ordinal function.  A strictly monotone
       function is one that is constantly increasing across the ordinals. $)
    csmo $a wff Smo A $.

    $d x y A $.
    $( Definition of a strictly monotone ordinal function.  Definition 7.46 in
       [TakeutiZaring] p. 50. $)
    df-smo $a |- ( Smo A <-> ( A : dom A --> On /\ Ord dom A /\ A. x e. dom A
       A. y e. dom A ( x e. y -> ( A ` x ) e. ( A ` y ) ) ) ) $.
  $}

  ${
    $d x y A $.
    issmo.1 $e |- A : B --> On $.
    issmo.2 $e |- Ord B $.
    issmo.3 $e |- ( ( x e. B /\ y e. B ) -> ( x e. y -> ( A ` x ) e. ( A ` y
       ) ) ) $.
    issmo.4 $e |- dom A = B $.
    $( Conditions for which ` A ` is a strictly monotone ordinal function. $)
    issmo $p |- Smo A $=
      ( csmo cdm con0 wf word cv wcel cfv wi wral df-smo feq2i mpbir wceq wb 
      ordeq ax-mp eleq2i syl2anb rgen2a mpbir3an ) CICJZKCLZUJMZANZBNZOUMCPUNCP
      OQZBUJRAUJRABCSUKDKCLEUJDKCHTUAULDMZFUJDUBULUPUCHUJDUDUEUAUOABUJUMDOUNDOU
      OUMUJOUNUJOGUJDUMHUFUJDUNHUFUGUHUI $.
      $( [19-Nov-2011] $) $( [15-Nov-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.
    $( Equality theorem for strictly monotone functions. $)
    smoeq $p |- ( A = B -> ( Smo A <-> Smo B ) ) $=
      ( vx vy wceq cdm con0 wf word cv wcel cfv wi wral w3a csmo id dmeq 
      feq12d wb ordeq syl fveq1 eleq12d imbi2d 2ralbidv raleq ralbidv 3bitrd 
      3anbi123d df-smo 3bitr4g ) ABEZAFZGAHZUNIZCJZDJZKZUQALZURALZKZMZDUNNCUNNZ
      OBFZGBHZVEIZUSUQBLZURBLZKZMZDVENZCVENZOAPBPUMUOVFUPVGVDVMUMUNVEGABUMQABRZ
      SUMUNVEEZUPVGTVNUNVEUAUBUMVDVKDUNNZCUNNVLCUNNZVMUMVCVKCDUNUNUMVBVJUSUMUTV
      HVAVIUQABUCURABUCUDUEUFUMVPVLCUNUMVOVPVLTVNVKDUNVEUGUBUHUMVOVQVMTVNVLCUNV
      EUGUBUIUJCDAUKCDBUKUL $.
      $( [19-Nov-2011] $) $( [16-Nov-2011] $) 

    $( The domain of a strictly monotone function is an ordinal. $)
    smodm $p |- ( Smo A -> Ord dom A )
    $=
      ( vx vy csmo cdm con0 wf word cv wcel cfv wi wral w3a df-smo 3simp2 
      sylbi ) ADAEZFAGZRHZBIZCIZJUAAKUBAKJLCRMBRMZNTBCAOSTUCPQ $.
      $( [19-Nov-2011] $) $( [16-Nov-2011] $) 

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone. $)
    smores $p |- ( ( Smo A /\ B e. dom A ) -> Smo ( A |` B ) ) $=
      ( vx vy cdm wcel csmo cres con0 wf word cv cfv wi wral w3a wfn crn wss 
      wa wfun funres funfn 3imtr3i resss rnss ax-mp sstr mpan anim12i df-f 
      3imtr4i a1i cin ordelord expcom ordin ex syli wceq wb dmres ordeq 
      syl6ibr hbra1 ax-17 hbral pm2.27 fvres adantr adantl eleq12d biimprcd 
      imim2i com23 syl6 impcom ra42 syl5 an4s com12 elin anbi12i syl5ib eleq2i 
      exp3a r19.21ad r19.21ai 3anim123d df-smo 3imtr4g ) BAEZFZAGZABHZGZWMWLIAJ
      ZWLKZCLZDLZFZWSAMZWTAMZFZNZDWLOZCWLOZPWOEZIWOJZXHKZXAWSWOMZWTWOMZFZNZDXHO
      ZCXHOZPWNWPWMWQXIWRXJXGXPWQXINWMAWLQZARZISZTWOXHQZWORZISZTWQXIXQXTXSYBAUA
      WOUAXQXTBAUBAUCWOUCUDYAXRSZXSYBWOASYCABUEWOAUFUGYAXRIUHUIUJWLIAUKXHIWOUKU
      LUMWMWRBWLUNZKZXJWRWMBKZYEWRWMYFWLBUOUPYFWRYEBWLUQURUSXHYDUTXJYEVAABVBZXH
      YDVCUGVDXGXPNWMXGXOCXHXFCWLVEXGWSXHFZXNDXHXFDCWLWSWLFZDVFXEDWLVEVGYHDVFXG
      YHWTXHFZXNXGWSYDFZWTYDFZTZXNYHYJTXGWSBFZYITZWTBFZWTWLFZTZTZXNYMYSXGXNYNYP
      YIYQXGXNNYNYPTZYIYQTZTUUAXENZXNXGUUAYTUUBXNNUUAUUBYTXNUUAUUBXEYTXNNUUAXEV
      HXEXAYTXMXDYTXMNXAYTXMXDYTXKXBXLXCYNXKXBUTYPWSBAVIVJYPXLXCUTYNWTBAVIVKVLV
      MVNVOVPVOVQXECDWLWLVRVSVTWAYKYOYLYRWSBWLWBWTBWLWBWCWDYHYKYJYLXHYDWSYGWEXH
      YDWTYGWEWCWDWFWGWHUMWICDAWJCDWOWJWKVQ $.
      $( [19-Nov-2011] $) $( [16-Nov-2011] $)

    $( A strictly monotone function restricted to an ordinal remains strictly
       monotone. $)
    smores2 $p |- ( ( Smo ( A |` B ) /\ C e. ( dom A i^i B ) /\ Ord B ) -> Smo
       ( A |` C ) ) $=
      ( cres csmo cdm cin wcel word w3a smores dmres incom eqtri eleq2i 
      sylan2br 3adant3 wss wceq wb ordelss ancoms inss2 sseli sylan 3adant1 
      resabs1 smoeq 3syl mpbid ) ABDZEZCAFZBGZHZBIZJZUKCDZEZACDZEZULUOUSUPULCUK
      FZHUSUOUKCKVBUNCVBBUMGUNABLBUMMNOPQUQCBRZURUTSUSVATUOUPVCULCBHZUPVCUOUPVD
      VCBCUAUBUNBCUMBUCUDUEUFACBUGURUTUHUIUJ $.
      $( [19-Nov-2011] $) $( [19-Nov-2011] $)
  $}

  ${
    $d x y A $.
    iordsmo.1 $e |- Ord A $.
    $( The identity relation restricted to the ordinals is a strictly monotone
       function. $)
    iordsmo $p |- Smo ( _I |` A ) $=
      ( vx vy cid cres con0 wf wfn crn wss df-f fnresi rnresi word ordsson 
      ax-mp eqsstri mpbir2an cv wcel wa cfv wceq fvresi adantr adantl eleq12d 
      biimprd dmresi issmo ) CDEAFZAAGULHULAIULJZGKAGULLAMUMAGANAOAGKBAPQRSBCTZ
      AUAZDTZAUAZUBZUNULUCZUPULUCZUAUNUPUAURUSUNUTUPUOUSUNUDUQAUNUEUFUQUTUPUDUO
      AUPUEUGUHUIAUJUK $.
      $( [19-Nov-2011] $) $( [16-Nov-2011] $)
  $}

$( (End of Andrew Salmon's mathbox.) $)

