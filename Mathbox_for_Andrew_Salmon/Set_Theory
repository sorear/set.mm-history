$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Set Theory
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.
    $( Any set that contains one element less than the universe is not equal to
       it. $)
    elnev $p |- ( A e. _V <-> { x | -. x = A } =/= _V ) $=
      ( cv wceq wex wn cab cvv wcel wne wal df-v eqeq2i wb eq2ab equid tbt
      albii bitr4i alnex 3bitri con2bii isset df-ne 3bitr4i ) ACZBDZAEZUGFZAGZH
      DZFBHIUJHJUKUHUKUJUFUFDZAGZDZUIAKZUHFHUMUJALMUNUIULNZAKUOUIULAOUIUPAULUIA
      PQRSUGATUAUBABUCUJHUDUE $.
      $( [16-Jun-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A y $.  $d x y $.
    isseta.1 $e |- ( y e. A -> A. x y e. A ) $.
    isseta.2 $e |- ( x = A -> A. y x = A ) $.
    $( A version of isset that does not require x and A to be distinct. $)
    isseta $p |- ( A e. _V <-> E. x x = A ) $=
      ( cvv wcel cv wceq wex isset hbeleq eqeq1 cbvex bitri ) CFGBHZCIZBJAHZCIZ
      AJBCKQSBAABCDLEPRCMNO $.
      $( [6-Jun-2011] $) $( [6-Jun-2011] $)
  $}

  ${
    $d x y $.  $d y A $.
    cla4gft.1 $e |- ( ps -> A. x ps ) $.
    cla4gft.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( A closed version of cla4gf. $)
    cla4gft $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                      ( A. x ph -> ps ) ) ) $=
      ( cv wceq wb wi wal cvv wcel wex hba1 bi1 imim2i a4s 19.22d ax-17 isseta
      syl5ib 19.36 syl6ib elisset syl5 ) CIEJZABKZLZCMZENOZACMBLZEFOULUMABLZCPZ
      UNULUICPUPUMULUIUOCUKCQUKUIUOLCUJUOUIABRSTUACDEHUIDUBUCUDABCGUEUFEFUGUH
      $.
      $( [6-Jun-2011] $) $( [6-Jun-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d A z $.  $d x z $.
    rcla4t.1 $e |- ( ps -> A. x ps ) $.
    $( A closed version of rcla4. $)
    rcla4t $p |- ( A. x ( x = A -> ( ph <-> ps ) ) -> ( A e. B ->
                   ( A. x e. B ph -> ps ) ) ) $=
      ( vz cv wceq wb wi wal wcel wral wa eleq1 adantr pm3.27 imbi12d ex a2i
      19.20i ax-17 hbim cla4gft syl df-ral syl7ib com34 pm2.43d ) CHZDIZABJZKZC
      LZDEMZACENZBKUOUPUQUPBUOUPUKEMZAKZCLZUPBKZUQUOULUSVAJZKZCLUPUTVAKKUNVCCUL
      UMVBULUMVBULUMOURUPABULURUPJUMUKDEPQULUMRSTUAUBUSVACGDEUPBCUPCUCFUDGHDMCU
      CUEUFACEUGUHUIUJ $.
      $( [6-Jun-2011] $) $( [6-Jun-2011] $)
  $}

  ${
    $( Distribute proper substitution through negated membership. $)
    sbcnel12g $p |- ( A e. D -> ( [ A / x ] B e/ C <-> [_ A / x ]_ B e/
                      [_ A / x ]_ C ) ) $=
      ( wcel wnel wsbc wn csb df-nel sbcbii sbcng sbcel12g notbid syl6bbr
      3bitrd ) BEFZCDGZABHCDFZIZABHTABHZIZABCJZABDJZGZSUAABECDKLTABEMRUCUDUEFZI
      UFRUBUGABCDENOUDUEKPQ $.
      $( [28-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $( Distribute proper substitution through an inequality. $)
    sbcne12g $p |- ( A e. D -> ( [ A / x ] B =/= C <-> [_ A / x ]_ B =/=
                     [_ A / x ]_ C ) ) $=
      ( wcel wne wsbc csb wn wceq nne sbcbii sbcng sbceqdig syl6bbr 3bitr3d
      con4bid ) BEFZCDGZABHZABCIZABDIZGZSTJZABHCDKZABHZUAJUDJZUEUFABECDLMTABENS
      UGUBUCKUHABCDEOUBUCLPQR $.
      $( [28-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $( A version of Russell's paradox which is proven using proper
       substitution. $)
    rusbcALT $p |- { x | x e/ x } e/ _V $=
      ( cv wnel cab cvv wcel wn wb pm5.19 wsbc csb elabsg sbcnel12g csbvarg
      eleq12d notbid df-nel syl5bb 3bitrd mto mpbir ) ABZUBCZADZECUDEFZGUEUDUDF
      ZUFGZHUFIUEUFUCAUDJAUDUBKZUHCZUGUCAUDELAUDUBUBEMUEUHUHFZGUGUIUEUJUFUEUHUD
      UHUDAUDENZUKOPUHUHQRSTUDEQUA $.
      $( [29-Jun-2011] $) $( [18-Jun-2011] $)
  $}

  ${
    $d x v z $.  $d y v z $.  $d A w v z $.  $d B w v z $.  $d ph v z $.
    $d ps v z $.
    cbvralcsf.1 $e |- ( x = y -> A = B ) $.
    cbvralcsf.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbvralcsf.3 $e |- ( w e. A -> A. y w e. A ) $.
    cbvralcsf.4 $e |- ( w e. B -> A. x w e. B ) $.
    cbvralcsf.5 $e |- ( ph -> A. y ph ) $.
    cbvralcsf.6 $e |- ( ps -> A. x ps ) $.
    $( A more general version of ~ cbvralf that doesn't require A and B to be
       distinct from x or y.  Changes bound variables using implicit
       substitution. $)
    cbvralcsf $p |- ( A. x e. A ph <-> A. y e. B ps ) $=
      ( vz vv cv wcel wi wal wral csb wsbc ax-17 visset hbcsb1 hbel hbs1 hbim
      wceq id csbeq1a eleq12d sbequ12 imbi12d cbval cvv hblem hbcsbg ax-mp
      hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ
      syl6bb bitri df-ral 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVRNPZCWCFUA
      ZQZACWCUBZRZNSWBVQWGCNVQNUCWEWFCCOOWCWDOPZWCQZCUCZCOWCFNUDZWJUEUFACNUGUHV
      OWCUIZVPWEAWFWLVOWCFWDWLUJCWCFUKULACNUMUNUOWGWANDWEWFDDOOWCWDWIDUCZWCUPQW
      HWDQZWNDSRWKDCOWCFUPWMDEOFJUQURUSUFACNDLUTUHWANUCWCVSUIZWEVTWFBWOWCVSWDGW
      OUJWOWDCVSFUAZGCWCVSFVAWPWHFQZCVSUBZOVBGCOVSFVCWROGWRWHGQZWQWSCDCEOGKUQVO
      VSUIFGWHHVDVEVFVGVHVIULWOWFACVSUBBANDCVJABCDMIVEVKUNUOVLACFVMBDGVMVN $.
      $( [13-Jul-2011] $) $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrexf that has no distinct variable
       restrictions.  Changes bound variables using implicit substitution. $)
    cbvrexcsf $p |- ( E. x e. A ph <-> E. y e. B ps ) $=
      ( vz vv cv wcel wa wex wrex csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbvex cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq
      sbequ syl6bb bitri df-rex 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVTNPZ
      CWEFUAZQZACWEUBZRZNSWDVSWICNVSNUCWGWHCCOOWEWFOPZWEQZCUCZCOWEFNUDZWLUEUFAC
      NUGUHVQWEUIZVRWGAWHWNVQWEFWFWNUJCWEFUKULACNUMUNUOWIWCNDWGWHDDOOWEWFWKDUCZ
      WEUPQWJWFQZWPDUQURWMDCOWEFUPWODEOFJUSUTVAUFACNDLVBUHWCNUCWEWAUIZWGWBWHBWQ
      WEWAWFGWQUJWQWFCWAFUAZGCWEWAFVCWRWJFQZCWAUBZOVDGCOWAFVEWTOGWTWJGQZWSXACDC
      EOGKUSVQWAUIFGWJHVFVGVHVIVJVKULWQWHACWAUBBANDCVLABCDMIVGVMUNUOVNACFVOBDGV
      OVP $.
      $( [13-Jul-2011] $) $( [13-Jul-2011] $)

    $( A more general version of ~ cbvreuv that has no distinct variable
       rextrictions.  Changes bound variables using implicit substitution. $)
    cbvreucsf $p |- ( E! x e. A ph <-> E! y e. B ps ) $=
      ( vz vv cv wcel wa weu wreu csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbveu cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq
      sbequ syl6bb bitri df-reu 3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVTNPZ
      CWEFUAZQZACWEUBZRZNSWDVSWICNVSNUCWGWHCCOOWEWFOPZWEQZCUCZCOWEFNUDZWLUEUFAC
      NUGUHVQWEUIZVRWGAWHWNVQWEFWFWNUJCWEFUKULACNUMUNUOWIWCNDWGWHDDOOWEWFWKDUCZ
      WEUPQWJWFQZWPDUQURWMDCOWEFUPWODEOFJUSUTVAUFACNDLVBUHWCNUCWEWAUIZWGWBWHBWQ
      WEWAWFGWQUJWQWFCWAFUAZGCWEWAFVCWRWJFQZCWAUBZOVDGCOWAFVEWTOGWTWJGQZWSXACDC
      EOGKUSVQWAUIFGWJHVFVGVHVIVJVKULWQWHACWAUBBANDCVLABCDMIVGVMUNUOVNACFVOBDGV
      OVP $.
      $( [13-Jul-2011] $) $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrab with no distinct variable
       restrictions. $)
    cbvrabcsf $p |- { x e. A | ph } = { y e. B | ps } $=
      ( vz vv cv wcel wa cab crab csb wsbc ax-17 visset hbcsb1 hbel hbs1 hban
      wceq id csbeq1a eleq12d sbequ12 anbi12d cbvab cvv wal wi hblem hbcsbg
      ax-mp hbsb csbeq1 df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ
      syl6bb eqtri df-rab 3eqtr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFTBDGTVSNPZCWDFUA
      ZQZACWDUBZRZNSWCVRWHCNVRNUCWFWGCCOOWDWEOPZWDQZCUCZCOWDFNUDZWKUEUFACNUGUHV
      PWDUIZVQWFAWGWMVPWDFWEWMUJCWDFUKULACNUMUNUOWHWBNDWFWGDDOOWDWEWJDUCZWDUPQW
      IWEQZWODUQURWLDCOWDFUPWNDEOFJUSUTVAUFACNDLVBUHWBNUCWDVTUIZWFWAWGBWPWDVTWE
      GWPUJWPWECVTFUAZGCWDVTFVCWQWIFQZCVTUBZOSGCOVTFVDWSOGWSWIGQZWRWTCDCEOGKUSV
      PVTUIFGWIHVEVFVGVHVIVJULWPWGACVTUBBANDCVKABCDMIVFVLUNUOVMACFVNBDGVNVO $.
      $( [13-Jul-2011] $) $( [13-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( Equivalence between two ways of saying "is a member of the complement of
       ` A ` ." $)
    compel $p |- ( x e. ( _V \ A ) <-> -. x e. A ) $=
      ( cv cvv cdif wcel wn wa eldif visset biantrur bitr4i ) ACZDBEFMDFZMBFGZH
      OMDBINOAJKL $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)

    $( Equality between two ways of saying "the complement of ` A ` ." $)
    compeq $p |- ( _V \ A ) = { x | -. x e. A } $=
      ( cv wcel wn cvv cdif compel abbi2i ) ACBDEAFBGABHI $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)
  $}

  ${
    $d A x y $.
    $( The complement of ` A ` is not equal to ` A ` . $)
    compne $p |- ( _V \ A ) =/= A $=
      ( vx vy cvv cdif wne wceq wn cv wcel cab wb wal wsbc pm5.19 ax-4 df-clab
      bibi12i sbn bibi1i bicom 3bitri sylib mto dfcleq mtbir compeq eqcomi
      abid2 eqeq12i mtbi df-ne mpbir ) DAEZAFUNAGZHBIAJZHZBKZUPBKZGZUOUTCIZURJZ
      VAUSJZLZCMZVEUPBVANZVFHZLZVFOVEVDVHVDCPVDUQBVANZVFLVGVFLVHVBVIVCVFUQCBQUP
      CBQRVIVGVFUPBCSTVGVFUAUBUCUDCURUSUEUFURUNUSAUNURBAUGUHBAUIUJUKUNAULUM $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)
  $}

  ${
    $d x ph $.  $d x z $.
    $( Two ways of saying "the complement of a class abstraction". $)
    compab $p |- ( _V \ { z | ph } ) = { z | -. ph } $=
      ( vx cvv cab cdif cv wcel wn compeq hbab1 hbn ax-17 wceq wsbc sbequ12r
      df-clab syl5bb notbid cbvab eqtri ) DABEZFCGZUBHZIZCEAIZBECUBJUEUFCBUDBAB
      CKLUFCMUCBGNZUDAUGABUCOAUDACBPACBQRSTUA $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)
  $}

  ${
    $d A x $.  $d B x $.
    $( Contrpositive law for subsets. $)
    conss34 $p |- ( A C_ B <-> ( _V \ B ) C_ ( _V \ A ) ) $=
      ( vx cv wcel wi wal cvv cdif wss wn con34b compel imbi12i bitr4i albii
      dfss2 3bitr4i ) CDZAEZSBEZFZCGSHBIZEZSHAIZEZFZCGABJUCUEJUBUGCUBUAKZTKZFUG
      TUALUDUHUFUICBMCAMNOPCABQCUCUEQR $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)

    $( Contrapositive law for subsets. $)
    conss2 $p |- ( A C_ ( _V \ B ) <-> B C_ ( _V \ A ) ) $=
      ( cvv wss cdif wb ssv ssconb mp2an ) ACDBCDACBEDBCAEDFAGBGABCHI $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)

    $( Contrapositive law for subsets. $)
    conss1 $p |- ( ( _V \ A ) C_ B <-> ( _V \ B ) C_ A ) $=
      ( vx cv cvv cdif wcel wi wal wss wn con1b compel imbi1i 3bitr4i albii
      dfss2 ) CDZEAFZGZRBGZHZCIREBFZGZRAGZHZCISBJUCAJUBUFCUEKZUAHUAKZUEHUBUFUEU
      ALTUGUACAMNUDUHUECBMNOPCSBQCUCAQO $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)
  $}

  ${
    ralbidar.1 $e |- ( ph -> A. x e. A ph ) $.
    ralbidar.2 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( More general form of ~ ralbida . $)
    ralbidar $p |- ( ph -> ( A. x e. A ps <-> A. x e. A ch ) ) $=
      ( cv wcel wi wal wral wb ex r19.20si syl df-ral sylib pm2.43 pm5.74d
      19.20i 19.15 3syl 3bitr4g ) ADHEIZBJZDKZUECJZDKZBDELCDELAUEUEBCMZJZJZDKZU
      FUHMZDKUGUIMAUKDELZUMAADELUOFAUKDEAUEUJGNOPUKDEQRULUNDULUEBCUEUJSTUAUFUHD
      UBUCBDEQCDEQUD $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)

    $( More general form of ~ rexbida . $)
    rexbidar $p |- ( ph -> ( E. x e. A ps <-> E. x e. A ch ) ) $=
      ( cv wcel wa wex wrex wb wi wal wral ex r19.20si syl df-ral sylib pm2.43
      pm5.32d 19.20i 19.18 3syl df-rex 3bitr4g ) ADHEIZBJZDKZUICJZDKZBDELCDELAU
      IUIBCMZNZNZDOZUJULMZDOUKUMMAUODEPZUQAADEPUSFAUODEAUIUNGQRSUODETUAUPURDUPU
      IBCUIUNUBUCUDUJULDUEUFBDEUGCDEUGUH $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)
  $}

  ${
    $d ph w $.  $d w x $.  $d w y $.  $d w z $.
    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction. $)
    dropab1 $p |- ( A. x x = y -> { <. x , z >. | ph } =
        { <. y , z >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq1 a4s eqeq2d anbi1d drex2
      drex1 abbidv df-opab 3eqtr4g ) BFZCFZGZBHZEFZUCDFZIZGZAJZDKZBKZELUGUDUHIZ
      GZAJZDKZCKZELABDMACDMUFUMUREULUQBCUKUPBCDUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOP
      QRSTABDEUAACDEUAUB $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)

    $( Theorem to aid use of the distinctor reduction theorem with ordered pair
       class abstraction. $)
    dropab2 $p |- ( A. x x = y -> { <. z , x >. | ph } =
        { <. z , y >. | ph } ) $=
      ( vw cv wceq wal cop wa wex cab copab opeq2 a4s eqeq2d anbi1d drex1
      drex2 abbidv df-opab 3eqtr4g ) BFZCFZGZBHZEFZDFZUCIZGZAJZBKZDKZELUGUHUDIZ
      GZAJZCKZDKZELADBMADCMUFUMUREULUQBCDUKUPBCUFUJUOAUFUIUNUGUEUIUNGBUCUDUHNOP
      QRSTADBEUAADCEUAUB $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)
  $}

  ${
    $d x A $.
    $( If the identity relation partially orders any class, then that class is
       the null class. $)
    ipo0 $p |- ( _I Po A <-> A = (/) ) $=
      ( vx cid wpo c0 wceq wne cv wcel wex wn n0 wbr weq equid visset ideq
      mpbir poirr ex mt2i con2i 19.23aiv sylbi necon4ai po0 poeq2 mpbiri
      impbii ) ACDZAEFZUJAEAEGBHZAIZBJUJKZBALUMUNBUJUMUJUMULULCMZUOBBNBOULULBPQ
      RUJUMUOKAULCSTUAUBUCUDUEUKUJECDCUFAECUGUHUI $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)

    $( A class that is founded by the identity relation is null. $)
    ifr0 $p |- ( _I Fr A <-> A = (/) ) $=
      ( vx cid wfr c0 wceq wne cv wcel wex wn n0 wbr equid visset ideq mpbir
      frirr ex mt2i con2i 19.23aiv sylbi necon4ai fr0 freq2 mpbiri impbii ) ACD
      ZAEFZUIAEAEGBHZAIZBJUIKZBALULUMBUIULUIULUKUKCMZUNUKUKFBNUKUKBOPQUIULUNKBA
      CRSTUAUBUCUDUJUIECDCUEAECUFUGUH $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)

    $( ~ ordelpss with an antecedent removed. $)
    ordpss $p |- ( Ord B -> ( A e. B -> A C. B ) ) $=
      ( word wcel wa wpss ordelord ex ancrd wb ordelpss ancoms biimpd expimpd
      syld ) BCZABDZACZQEABFZPQRPQRBAGHIPRQSPREQSRPQSJABKLMNO $.
      $( [25-Jul-2011] $) $( [25-Jul-2011] $)
  $}

  ${
    $d y x F $.  $d y x A $.
    fviota.1 $e |- A e. _V $.
    $( Alternate definition of the value of a function.  The value of a
       function expressed using ` iota ` . $)
    fviota $p |- ( F ` A ) = ( iota x A F x ) $=
      ( vy cfv cv wbr wceq wb wal cab cuni ciota fv2 dfiota2 eqtr4i ) BCFBAGZCH
      ZREGIJAKELMSANEABCDOSAEPQ $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    fvsb.1 $e |- A e. _V $.
    $( Explicit substitution of a value of a function into a wff. $)
    fvsb $p |- ( E! y A F y -> ( [ ( F ` A ) / x ] ph <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cv wbr weu ciota wsbc wceq wb wal wa wex cfv iotasbc fviota dfsbcq
      ax-mp syl5bb ) DCGZEHZCIABUDCJZKZUDUCBGLMCNAOBPABDEQZKZUDACBRUGUELUHUFMCD
      EFSABUGUETUAUB $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)

    fveqsb.2 $e |- ( x = ( F ` A ) -> ( ph <-> ps ) ) $.
    fveqsb.3 $e |- ( ps -> A. x ps ) $.
    $( Implicit substitution of a value of a function into a wff. $)
    fveqsb $p |- ( E! y A F y -> ( ps <->
        E. x ( A. y ( A F y <-> y = x ) /\ ph ) ) ) $=
      ( cv wbr weu cfv wsbc wceq wb wal wa wex fvsb cvv wcel fvex wi a1i
      sbciegf ax-mp syl5bbr ) EDJZFKZDLACEFMZNZUJUICJOPDQARCSBACDEFGTUKUAUBZULB
      PEFUCABCUKUABBCQUDUMIUEHUFUGUH $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)
  $}

  ${
    $d z w x $.  $d z w y $.  $d z w ph $.  $d z w ps $.
    cbviotaf.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbviotaf.2 $e |- ( ph -> A. y ph ) $.
    cbviotaf.3 $e |- ( ps -> A. x ps ) $.
    $( Changes bound variables in a description. $)
    cbviotaf $p |- ( iota x ph ) = ( iota y ps ) $=
      ( vw vz cv wceq wb wal cab cuni ciota wsbc ax-17 hbs1 hbbi sbequ12
      equequ1 bibi12d cbval hbsb sbequ sbie syl6bb bitri abbii unieqi dfiota2
      3eqtr4i ) ACJZHJZKZLZCMZHNZOBDJZUOKZLZDMZHNZOACPBDPUSVDURVCHURACIJZQZVEUO
      KZLZIMVCUQVHCIUQIRVFVGCACISVGCRTUNVEKAVFUPVGACIUACIHUBUCUDVHVBIDVFVGDACID
      FUEVGDRTVBIRVEUTKZVFBVGVAVIVFACUTQBAIDCUFABCDGEUGUHIDHUBUCUDUIUJUKACHULBD
      HULUM $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)
  $}

  ${
    $d ph y $.  $d ps x $.
    cbviota.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Changes bound variables in a description. $)
    cbviota $p |- ( iota x ph ) = ( iota y ps ) $=
      ( ax-17 cbviotaf ) ABCDEADFBCFG $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)
  $}

  ${
    $( Membership law for descriptions.  See ~ reucl2 . $)
    iotacl $p |- ( E! x ph -> ( iota x ph ) e. { x | ph } ) $=
      ( weu ciota cab wcel wsbc iota3 cvv wb iotaexeu elabsg syl mpbird ) ABCZA
      BDZABEFZABPGZABHOPIFQRJABKABPILMN $.
      $( [1-Aug-2011] $) $( [1-Aug-2011] $)
  $}

$( (End of Andrew Salmon's mathbox.) $)

