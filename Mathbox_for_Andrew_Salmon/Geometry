$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +r -r .v PtDf RR3 plane3 line3 $.

  $( Introduce the operation of vector addition. $)
  plusrc $a class +r $.

  $( Introduce the operation of vector subtraction. $)
  minusrc $a class -r $.

  $( Introduce the operation of scalar multiplication. $)
  timesrc $a class .v $.

  $( ` PtDf ` is a predicate that is crucial for the definition of lines as
     well as proving a number of important theorems. $)
  ptdfc $a class PtDf ( A , B ) $.

  $( ` RR3 ` is a class. $)
  rr3c $a class RR3 $.

  $( ` plane3 ` is a class. $)
  cplane3 $a class plane3 $.

  $( ` line3 ` is a class. $)
  cline3 $a class line3 $.

  ${
    $d v w x y z A $.  $d v w x y z B $.
    $( Define the operation of vector addition. $)
    df-addr $a |- +r = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( ( x ` v ) + ( y ` v ) ) } |` RR ) } $.

    $( Define the operation of vector subtraction. $)
    df-subr $a |- -r = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( ( x ` v ) - ( y ` v ) ) } |` RR ) } $.

    $( Define the operation of scalar multiplication. $)
    df-mulv $a |- .v = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( x x. ( y ` v ) ) } |` RR ) } $.

    $( Value of the operation of vector addition. $)
    addrval $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) = ( { <. v , w >. | w =
        ( ( A ` v ) + ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv caddc co wceq copab cr cres plusrc wfun cvv wcel
      funopabeq reex resfunexg mp2an fveq1 opreq1d eqeq2d opabbidv reseq1 syl
      opreq2d df-addr oprabval5 ) GHICDEFAJZBJZGJZKZUPHJZKZLMZNZBAOZPQZUOUPCKZU
      PDKZLMZNZBAOZPQZRUOVEUTLMZNZBAOZPQZVISPTUAVJTUABAVGUBUCVIPTUDUEUQCNZVCVMN
      VDVNNVOVBVLBAVOVAVKUOVOURVEUTLUPUQCUFUGUHUIVCVMPUJUKUSDNZVMVINVNVJNVPVLVH
      BAVPVKVGUOVPUTVFVELUPUSDUFULUHUIVMVIPUJUKGHIABUMUN $.
      $( [27-Jan-2012] $)

    $( Value of the operation of vector subtraction. $)
    subrval $p |- ( ( A e. C /\ B e. D ) -> ( A -r B ) = ( { <. v , w >. | w =
        ( ( A ` v ) - ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv cmin co wceq copab cr cres minusrc wfun cvv wcel
      funopabeq reex resfunexg mp2an fveq1 opreq1d eqeq2d opabbidv reseq1 syl
      opreq2d df-subr oprabval5 ) GHICDEFAJZBJZGJZKZUPHJZKZLMZNZBAOZPQZUOUPCKZU
      PDKZLMZNZBAOZPQZRUOVEUTLMZNZBAOZPQZVISPTUAVJTUABAVGUBUCVIPTUDUEUQCNZVCVMN
      VDVNNVOVBVLBAVOVAVKUOVOURVEUTLUPUQCUFUGUHUIVCVMPUJUKUSDNZVMVINVNVJNVPVLVH
      BAVPVKVGUOVPUTVFVELUPUSDUFULUHUIVMVIPUJUKGHIABUMUN $.
      $( [27-Jan-2012] $)

    $( Value of the operation of scalar multiplication. $)
    mulvval $p |- ( ( A e. C /\ B e. D ) -> ( A .v B ) = ( { <. v , w >. | w =
        ( A x. ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv cmul co wceq copab cr cres timesrc wfun cvv wcel
      funopabeq reex resfunexg mp2an opreq1 eqeq2d opabbidv reseq1 syl fveq1
      opreq2d df-mulv oprabval5 ) GHICDEFAJZGJZBJZHJZKZLMZNZBAOZPQZUOCUQDKZLMZN
      ZBAOZPQZRUOCUSLMZNZBAOZPQZVGSPTUAVHTUABAVEUBUCVGPTUDUEUPCNZVBVKNVCVLNVMVA
      VJBAVMUTVIUOUPCUSLUFUGUHVBVKPUIUJURDNZVKVGNVLVHNVNVJVFBAVNVIVEUOVNUSVDCLU
      QURDUKULUGUHVKVGPUIUJGHIABUMUN $.
      $( [27-Jan-2012] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w x y z C $.  $d x D $.
    $( Vector addition at a value.  The operation takes each vector ` A ` and
       ` B ` and forms a new vector whose values are the sum of each of the
       values of ` A ` and ` B ` . $)
    addrfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A +r B ) ` C ) =
        ( ( A ` C ) + ( B ` C ) ) ) $=
      ( vy vx wcel cr plusrc co cfv caddc wceq wa cv copab cres addrval fveq1d
      fveq2 opreq12d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZL
      ZCALZCBLZMKZNUHUIOZUJULCFPGPZALZUQBLZMKZNZGFQIRZLUOUPCUKVBFGABEDSTGFCUTUO
      IVBUQCNURUMUSUNMUQCAUAUQCBUAUBVAGFIUCUMUNMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $d v w x y z A $.  $d v w x y z B $.  $d v w C $.
    $( Vector subtraction at a value. $)
    subrfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A -r B ) ` C ) =
        ( ( A ` C ) - ( B ` C ) ) ) $=
      ( vy vx wcel cr minusrc co cfv cmin wceq wa cv copab cres subrval fveq1d
      fveq2 opreq12d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZL
      ZCALZCBLZMKZNUHUIOZUJULCFPGPZALZUQBLZMKZNZGFQIRZLUOUPCUKVBFGABEDSTGFCUTUO
      IVBUQCNURUMUSUNMUQCAUAUQCBUAUBVAGFIUCUMUNMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $( Scalar multiplication at a value. $)
    mulvfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A .v B ) ` C ) =
        ( A x. ( B ` C ) ) ) $=
      ( vy vx wcel cr timesrc co cfv cmul wceq wa cv copab cres mulvval fveq1d
      fveq2 opreq2d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZLZ
      ACBLZMKZNUHUIOZUJULCFPAGPZBLZMKZNZGFQIRZLUNUOCUKUTFGABEDSTGFCURUNIUTUPCNU
      QUMAMUPCBUAUBUSGFIUCAUMMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $( Vector addition produces a function. $)
    addrfn $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) Fn RR ) $=
      ( vy vx wcel wa plusrc co cr wfn cv cfv caddc wceq copab cres oprex
      resopab fnopab2 addrval fneq1d mpbiri ) ACGBDGHZABIJZKLEMFMZANZUGBNZOJZPZ
      FEQKRZKLFEKUJULUHUIOSUKFEKTUAUEKUFULEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Vector subtraction produces a function. $)
    subrfn $p |- ( ( A e. C /\ B e. D ) -> ( A -r B ) Fn RR ) $=
      ( vy vx wcel wa minusrc co cr wfn cv cfv cmin wceq copab cres oprex
      resopab fnopab2 subrval fneq1d mpbiri ) ACGBDGHZABIJZKLEMFMZANZUGBNZOJZPZ
      FEQKRZKLFEKUJULUHUIOSUKFEKTUAUEKUFULEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Scalar multiplication producees a function. $)
    mulvfn $p |- ( ( A e. C /\ B e. D ) -> ( A .v B ) Fn RR ) $=
      ( vy vx wcel wa timesrc co cr wfn cv cfv cmul wceq copab cres oprex
      resopab fnopab2 mulvval fneq1d mpbiri ) ACGBDGHZABIJZKLEMAFMBNZOJZPZFEQKR
      ZKLFEKUHUJAUGOSUIFEKTUAUEKUFUJEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Vector addition is commutative. $)
    addrcom $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) = ( B +r A ) ) $=
      ( vx wcel wa plusrc co cr wfn wceq addrfn ancoms jca cv cfv wral eqfnfv2
      w3a caddc fvex addcomgi a1i addrfv 3com12 3eqtr4d 3expia r19.21aiv
      syl5cbir mpd ) ACFZBDFZGZABHIZJKZBAHIZJKZGZUOUQLZUNUPURABCDMUMULURBADCMNO
      USUTEPZUOQZVAUQQZLZEJRUNEJUOUQSUNVDEJULUMVAJFZVDULUMVETZVAAQZVABQZUAIZVHV
      GUAIZVBVCVIVJLVFVGVHVAAUBVABUBUCUDABVADCUEUMULVEVCVJLBAVACDUEUFUGUHUIUJUK
      $.
      $( [28-Jan-2012] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    $( Define the predicate ` PtDf ` , which is a utility definition used to
       shorten definitions and simplify proofs. $)
    df-ptdf $a |- PtDf ( A , B ) = ( x e. RR |-> ( ( ( x .v ( B -r A ) )
        +v A ) " { 1 , 2 , 3 } ) ) $.

    $( Define the set of all points ` RR3 ` .  We define each point ` A ` as a
       function to allow the use of vector addition and subtraction as well as
       scalar multiplication in our proofs. $)
    df-rr3 $a |- RR3 = ( RR ^m { 1 , 2 , 3 } ) $.

    $( Define the set of all lines.  A line is an infinite subset of ` RR3 `
       that satisfies a ` PtDf ` property. $)
    df-line3 $a |- line3 = { x e. ~P RR3 | ( 2o ~<_ x /\
        A. y e. x A. z e. x ( z =/= y -> ran PtDf ( y , z ) = x ) ) } $.
  $}


$( (End of Andrew Salmon's mathbox.) $)


