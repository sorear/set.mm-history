$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c +r -r .v PtDf RR3 plane3 line3 $.

  $( Introduce the operation of vector addition. $)
  cplusr $a class +r $.

  $( Introduce the operation of vector subtraction. $)
  cminusr $a class -r $.

  $( Introduce the operation of scalar multiplication. $)
  ctimesr $a class .v $.

  $( ` PtDf ` is a predicate that is crucial for the definition of lines as
     well as proving a number of important theorems. $)
  cptdfc $a class PtDf ( A , B ) $.

  $( ` RR3 ` is a class. $)
  crr3c $a class RR3 $.

  $( ` plane3 ` is a class. $)
  cplane3 $a class plane3 $.

  $( ` line3 ` is a class. $)
  cline3 $a class line3 $.

  ${
    $d v w x y z A $.  $d v w x y z B $.
    $( Define the operation of vector addition. $)
    df-addr $a |- +r = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( ( x ` v ) + ( y ` v ) ) } |` RR ) } $.

    $( Define the operation of vector subtraction. $)
    df-subr $a |- -r = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( ( x ` v ) - ( y ` v ) ) } |` RR ) } $.

    $( Define the operation of scalar multiplication. $)
    df-mulv $a |- .v = { <. <. x , y >. , z >. | z = ( { <. v , w >. | w =
        ( x x. ( y ` v ) ) } |` RR ) } $.

    $( Value of the operation of vector addition.  (Contributed by Andrew
       Salmon, 27-Jan-2012.) $)
    addrval $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) = ( { <. v , w >. | w =
        ( ( A ` v ) + ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv caddc co wceq copab cr cres cvv wcel fveq1 cplusr mp2an
      wfun funopabeq reex resfunexg opreq1d eqeq2d opabbidv syl opreq2d df-addr
      reseq1 oprabval5 ) GHICDEFAJZBJZGJZKZUPHJZKZLMZNZBAOZPQZUOUPCKZUPDKZLMZNZ
      BAOZPQZUAUOVEUTLMZNZBAOZPQZVIUCPRSVJRSBAVGUDUEVIPRUFUBUQCNZVCVMNVDVNNVOVB
      VLBAVOVAVKUOVOURVEUTLUPUQCTUGUHUIVCVMPUMUJUSDNZVMVINVNVJNVPVLVHBAVPVKVGUO
      VPUTVFVELUPUSDTUKUHUIVMVIPUMUJGHIABULUN $.
      $( [27-Jan-2012] $)

    $( Value of the operation of vector subtraction.  (Contributed by Andrew
       Salmon, 27-Jan-2012.) $)
    subrval $p |- ( ( A e. C /\ B e. D ) -> ( A -r B ) = ( { <. v , w >. | w =
        ( ( A ` v ) - ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv cmin co wceq copab cr cres cvv wcel fveq1 cminusr mp2an
      wfun funopabeq reex resfunexg opreq1d eqeq2d opabbidv syl opreq2d df-subr
      reseq1 oprabval5 ) GHICDEFAJZBJZGJZKZUPHJZKZLMZNZBAOZPQZUOUPCKZUPDKZLMZNZ
      BAOZPQZUAUOVEUTLMZNZBAOZPQZVIUCPRSVJRSBAVGUDUEVIPRUFUBUQCNZVCVMNVDVNNVOVB
      VLBAVOVAVKUOVOURVEUTLUPUQCTUGUHUIVCVMPUMUJUSDNZVMVINVNVJNVPVLVHBAVPVKVGUO
      VPUTVFVELUPUSDTUKUHUIVMVIPUMUJGHIABULUN $.
      $( [27-Jan-2012] $)

    $( Value of the operation of scalar multiplication.  (Contributed by Andrew
       Salmon, 27-Jan-2012.) $)
    mulvval $p |- ( ( A e. C /\ B e. D ) -> ( A .v B ) = ( { <. v , w >. | w =
        ( A x. ( B ` v ) ) } |` RR ) ) $=
      ( vx vy vz cv cfv cmul co wceq copab cr cres cvv wcel eqeq2d ctimesr wfun
      funopabeq reex resfunexg mp2an opreq1 opabbidv reseq1 syl opreq2d df-mulv
      fveq1 oprabval5 ) GHICDEFAJZGJZBJZHJZKZLMZNZBAOZPQZUOCUQDKZLMZNZBAOZPQZUA
      UOCUSLMZNZBAOZPQZVGUBPRSVHRSBAVEUCUDVGPRUEUFUPCNZVBVKNVCVLNVMVAVJBAVMUTVI
      UOUPCUSLUGTUHVBVKPUIUJURDNZVKVGNVLVHNVNVJVFBAVNVIVEUOVNUSVDCLUQURDUMUKTUH
      VKVGPUIUJGHIABULUN $.
      $( [27-Jan-2012] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w x y z C $.  $d x D $.
    $( Vector addition at a value.  The operation takes each vector ` A ` and
       ` B ` and forms a new vector whose values are the sum of each of the
       values of ` A ` and ` B ` .  (Contributed by Andrew Salmon,
       27-Jan-2012.) $)
    addrfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A +r B ) ` C ) =
        ( ( A ` C ) + ( B ` C ) ) ) $=
      ( vy vx wcel cr cplusr co cfv caddc wceq wa cv copab cres addrval fveq2
      fveq1d opreq12d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZ
      LZCALZCBLZMKZNUHUIOZUJULCFPGPZALZUQBLZMKZNZGFQIRZLUOUPCUKVBFGABEDSUAGFCUT
      UOIVBUQCNURUMUSUNMUQCATUQCBTUBVAGFIUCUMUNMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $d v w x y z A $.  $d v w x y z B $.  $d v w C $.
    $( Vector subtraction at a value.  (Contributed by Andrew Salmon,
       27-Jan-2012.) $)
    subrfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A -r B ) ` C ) =
        ( ( A ` C ) - ( B ` C ) ) ) $=
      ( vy vx wcel cr cminusr co cfv cmin wceq wa cv copab cres subrval fveq2
      fveq1d opreq12d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZ
      LZCALZCBLZMKZNUHUIOZUJULCFPGPZALZUQBLZMKZNZGFQIRZLUOUPCUKVBFGABEDSUAGFCUT
      UOIVBUQCNURUMUSUNMUQCATUQCBTUBVAGFIUCUMUNMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $( Scalar multiplication at a value.  (Contributed by Andrew Salmon,
       27-Jan-2012.) $)
    mulvfv $p |- ( ( A e. E /\ B e. D /\ C e. RR ) -> ( ( A .v B ) ` C ) =
        ( A x. ( B ` C ) ) ) $=
      ( vy vx wcel cr ctimesr co cfv cmul wceq wa cv copab cres mulvval fveq1d
      fveq2 opreq2d resopab oprex fvopab4 sylan9eq 3impa ) AEHZBDHZCIHZCABJKZLZ
      ACBLZMKZNUHUIOZUJULCFPAGPZBLZMKZNZGFQIRZLUNUOCUKUTFGABEDSTGFCURUNIUTUPCNU
      QUMAMUPCBUAUBUSGFIUCAUMMUDUEUFUG $.
      $( [27-Jan-2012] $)

    $( Vector addition produces a function.  (Contributed by Andrew Salmon,
       27-Jan-2012.) $)
    addrfn $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) Fn RR ) $=
      ( vy vx wcel wa cplusr co cr wfn cfv caddc wceq copab cres oprex resopab
      cv fnopab2 addrval fneq1d mpbiri ) ACGBDGHZABIJZKLETFTZAMZUGBMZNJZOZFEPKQ
      ZKLFEKUJULUHUINRUKFEKSUAUEKUFULEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Vector subtraction produces a function.  (Contributed by Andrew Salmon,
       27-Jan-2012.) $)
    subrfn $p |- ( ( A e. C /\ B e. D ) -> ( A -r B ) Fn RR ) $=
      ( vy vx wcel wa cminusr co cr wfn cfv cmin wceq copab cres oprex resopab
      cv fnopab2 subrval fneq1d mpbiri ) ACGBDGHZABIJZKLETFTZAMZUGBMZNJZOZFEPKQ
      ZKLFEKUJULUHUINRUKFEKSUAUEKUFULEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Scalar multiplication producees a function.  (Contributed by Andrew
       Salmon, 27-Jan-2012.) $)
    mulvfn $p |- ( ( A e. C /\ B e. D ) -> ( A .v B ) Fn RR ) $=
      ( vy vx wcel wa ctimesr co cr wfn cfv cmul wceq copab cres oprex resopab
      cv fnopab2 mulvval fneq1d mpbiri ) ACGBDGHZABIJZKLETAFTBMZNJZOZFEPKQZKLFE
      KUHUJAUGNRUIFEKSUAUEKUFUJEFABCDUBUCUD $.
      $( [27-Jan-2012] $)

    $( Vector addition is commutative.  (Contributed by Andrew Salmon,
       28-Jan-2012.) $)
    addrcom $p |- ( ( A e. C /\ B e. D ) -> ( A +r B ) = ( B +r A ) ) $=
      ( vx wcel wa cplusr co cr wfn wceq addrfn ancoms jca cv caddc fvex addrfv
      cfv wral w3a addcomgi a1i 3com12 3eqtr4d r19.21aiv eqfnfv2 syl5ibrcom mpd
      3expia ) ACFZBDFZGZABHIZJKZBAHIZJKZGZUOUQLZUNUPURABCDMUMULURBADCMNOUNUTUS
      EPZUOTZVAUQTZLZEJUAUNVDEJULUMVAJFZVDULUMVEUBZVAATZVABTZQIZVHVGQIZVBVCVIVJ
      LVFVGVHVAARVABRUCUDABVADCSUMULVEVCVJLBAVACDSUEUFUKUGEJUOUQUHUIUJ $.
      $( [28-Jan-2012] $)
  $}

  ${
    $d x y z A $.  $d x B $.
    $( Define the predicate ` PtDf ` , which is a utility definition used to
       shorten definitions and simplify proofs. $)
    df-ptdf $a |- PtDf ( A , B ) = ( x e. RR |-> ( ( ( x .v ( B -r A ) )
        +v A ) " { 1 , 2 , 3 } ) ) $.

    $( Define the set of all points ` RR3 ` .  We define each point ` A ` as a
       function to allow the use of vector addition and subtraction as well as
       scalar multiplication in our proofs. $)
    df-rr3 $a |- RR3 = ( RR ^m { 1 , 2 , 3 } ) $.

    $( Define the set of all lines.  A line is an infinite subset of ` RR3 `
       that satisfies a ` PtDf ` property. $)
    df-line3 $a |- line3 = { x e. ~P RR3 | ( 2o ~<_ x /\
        A. y e. x A. z e. x ( z =/= y -> ran PtDf ( y , z ) = x ) ) } $.
  $}


$( (End of Andrew Salmon's mathbox.) $)


