$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Principia Mathematica * 13 and * 14
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( One result of theorem *13.13 in [WhiteheadRussell] p. 178.  A note on the
     section - to make the theorems more usable, and because inequality is
     notation for set theory (it is not defined in the predicate calculus
     section), this section will use classes instead of sets. $)
  pm13.13a $p |- ( ( ph /\ x = A ) -> [ A / x ] ph ) $=
    ( cv wceq wsbc sbceq1a biimpac ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $)

  $( Theorem *13.13 in [WhiteheadRussell] p. 178 with different variable
     substitution. $)
  pm13.13b $p |- ( ( [ A / x ] ph /\ x = A ) -> ph ) $=
    ( cv wceq wsbc sbceq1a biimparc ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $)

  $( Theorem *13.14 in [WhiteheadRussell] p. 178. $)
  pm13.14 $p |- ( ( [ A / x ] ph /\ -. ph ) -> x =/= A ) $=
    ( wsbc wn cv wne wceq sbceq1a biimprcd necon3bd imp ) ABCDZAEBFZCGMANCNCHAM
    ABCIJKL $.
    $( [3-Jun-2011] $)

  $( Theorem *13.18 in [WhiteheadRussell] p. 178.  (Moved to ~ pm13.18 in main
     set.mm and may be deleted by mathbox owner, ATS. --NM 6-Sep-2011.) $)
  pm13.18OLD $p |- ( ( A = B /\ A =/= C ) -> B =/= C ) $=
    ( wceq wne eqeq1 biimprd necon3d imp ) ABDZACEBCEJBCACJACDBCDABCFGHI $.
    $( [3-Jun-2011] $)

  $( Theorem *13.181 in [WhiteheadRussell] p. 178.  (Moved to ~ pm13.181 in
     main set.mm and may be deleted by mathbox owner, ATS. --NM 6-Sep-2011.) $)
  pm13.181OLD $p |- ( ( A = B /\ B =/= C ) -> A =/= C ) $=
    ( wceq wne eqcom pm13.18 sylanb ) ABDBADBCEACEABFBACGH $.
    $( [3-Jun-2011] $)

  ${
    $d y A z $.  $d y B z $.
    $( Compare theorem *13.183 in [WhiteheadRussell] p. 178.  Only ` A ` is
       required to be a set. $)
    pm13.183 $p |- ( A e. C -> ( A = B <-> A. z ( z = A <-> z = B ) ) ) $=
      ( vy cv wceq wb wal eqeq1 eqeq2 bibi1d albidv 19.21aiv wsbc stdpc4 sbbi
      eqsb3 bibi2i equsb1 bi1 mpi sylbi syl impbii vtoclbg ) EFZCGZAFZUGGZUICGZ
      HZAIZBCGUIBGZUKHZAIEBDUGBCJUGBGZULUOAUPUJUNUKUGBUIKLMUHUMUHULAUGCUIKNUMUL
      AUGOZUHULAEPUQUJAUGOZUKAUGOZHZUHUJUKAEQUTURUHHZUHUSUHUREACRSVAURUHAETURUH
      UAUBUCUCUDUEUF $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d ph z $.
    $( Theorem *13.192 in [WhiteheadRussell] p. 179. $)
    pm13.192 $p |- ( A e. _V -> ( E. y ( A. x ( x = A <-> x = y ) /\ ph ) <->
      [ A / y ] ph ) ) $=
      ( cvv wcel wsbc cv wceq wa wex wb wal sbc5g wi bi2 alimi ax-17 eqeq1
      equsal biimpi syl eqeq2 eqcoms 19.21aiv impbii anbi1i exbii syl6rbbr ) DE
      FACDGCHZDIZAJZCKBHZDIZUMUJIZLZBMZAJZCKACDENURULCUQUKAUQUKUQUOUNOZBMZUKUPU
      SBUNUOPQUTUKUNUKBCUKBRUMUJDSTUAUBUKUPBUPDUJDUJUMUCUDUEUFUGUHUI $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  $( Theorem *13.193 in [WhiteheadRussell] p. 179. $)
  pm13.193 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ x = y ) ) $=
    ( weq wsb sbequ12 pm5.32ri ) BCDAABCEABCFG $.
    $( [3-Jun-2011] $)

  $( Theorem *13.194 in [WhiteheadRussell] p. 179. $)
  pm13.194 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ ph /\ x = y ) ) $=
    ( cv wceq wa wsbc w3a pm13.13a simpl simpr 3jca 3simpc impbii ) ABDCDZEZFZA
    BOGZAPHQRAPABOIAPJAPKLRAPMN $.
    $( [16-Jun-2011] $) $( [3-Jun-2011] $)

  ${
    $d y A $.
    pm13.195.1 $e |- A e. _V $.
    $( Theorem *13.195 in [WhiteheadRussell] p. 179.  This theorem is very
       similar to sbc5. $)
    pm13.195 $p |- ( E. y ( y = A /\ ph ) <-> [ A / y ] ph ) $=
      ( wsbc cv wceq wa wex sbc5 bicomi ) ABCEBFCGAHBIABCDJK $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *13.196 in [WhiteheadRussell] p. 179.  The only difference is
       the position of the substituted variable. $)
    pm13.196a $p |- ( -. ph <-> A. y ( [ y / x ] ph -> y =/= x ) ) $=
      ( wn cv wceq wsbc wa wex wi wal wne sbelx sb56 sbn imbi2i con2b df-ne
      3bitr2ri 3bitri albii ) ADZCEZBEZFZUBBUCGZHCIUEUFJZCKABUCGZUCUDLZJZCKUBCB
      MUFCBNUGUJCUGUEUHDZJUHUEDZJUJUFUKUEABCOPUEUHQULUIUHUIULUIULUCUDRZUMUMSPTU
      AT $.
      $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.21 in [WhiteheadRussell] p. 179. $)
    2sbc6g $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ( z = A /\
      w = B ) -> ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wi wal wsbc wb eqeq2 anbi2d imbi1d 2albidv ax-17
      dfsbcq sbbid bibi12d anbi1d 2sb6 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZE
      LZMZANZCOBOZACEPZBDPZQZUKHKZLZUMIKZLZMZANZCOBOZACVCPZBVAPZQVBUNMZANZCOBOZ
      URBVAPZQUTIHEDGFVCELZVGVLVIVMVNVFVKBCVNVEVJAVNVDUNVBVCEUMRSTUAVNVHURBHVNB
      UBACVCEUCUDUEVADLZVLUQVMUSVOVKUPBCVOVJUOAVOVBULUNVADUKRUFTUAURBVADUCUEVIV
      GABCHIUGUHUIUJ $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.22 in [WhiteheadRussell] p. 179. $)
    2sbc5g $p |- ( ( A e. C /\ B e. D ) -> ( E. z E. w ( ( z = A /\ w = B )
      /\ ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wex wsbc wb eqeq2 anbi2d anbi1d 2exbidv ax-17
      dfsbcq sbbid bibi12d 2sb5 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZELZMZAMZ
      CNBNZACEOZBDOZPZUIHKZLZUKIKZLZMZAMZCNBNZACVAOZBUSOZPUTULMZAMZCNBNZUPBUSOZ
      PURIHEDGFVAELZVEVJVGVKVLVDVIBCVLVCVHAVLVBULUTVAEUKQRSTVLVFUPBHVLBUAACVAEU
      BUCUDUSDLZVJUOVKUQVMVIUNBCVMVHUMAVMUTUJULUSDUIQSSTUPBUSDUBUDVGVEABCHIUEUF
      UGUH $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Equivalence between two different forms of ` iota ` . $)
    iotain $p |- ( E! x ph -> |^| { x | ph } = ( iota x ph ) ) $=
      ( vy weu cv wceq wb wal wex cab cint cio df-eu csn visset intsn hba1 ax4
      abbid df-sn syl6eqr inteqd iotaval 3eqtr4a 19.23aiv sylbi ) ABDABECEZFZGZ
      BHZCIABJZKZABLZFZABCMUJUNCUJUGNZKUGULUMUGCOPUJUKUOUJUKUHBJUOUJAUHBUIBQUIB
      RSBUGTUAUBABCUCUDUEUF $.
      $( [15-Jul-2011] $)
  $}

  ${
    $d y ph $.  $d y x $.
    $( The iota class exists.  This theorem does not require ~ ax-nul for its
       proof. $)
    iotaexeu $p |- ( E! x ph -> ( iota x ph ) e. _V ) $=
      ( vy cv wceq wb wal wex cio weu cvv wcel iotaval eqcomd eximi df-eu
      isset 3imtr4i ) ABDCDZEFBGZCHSABIZEZCHABJUAKLTUBCTUASABCMNOABCPCUAQR $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Definition *14.01 in [WhiteheadRussell] p. 184.  In Principia
       Mathematica, Russell and Whitehead define ` iota ` in terms of a
       function of ` ( iota x ph ) ` .  Their definition differs in that a
       function of ` ( iota x ph ) ` evaluates to "false" when there isn't a
       single ` x ` that satisfies ` ph ` . $)
    iotasbc $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
         E. y ( A. x ( ph <-> x = y ) /\ ps ) ) ) $=
      ( weu cio wsbc cv wceq wa wex wb wal cvv wcel iotaexeu sbc5g syl wi eueq
      sylib df-eu iotaval eqcomd ancri eximi sylbi eupick syl11anc impbid1
      anbi1d exbidv bitrd ) ACEZBDACFZGZDHZUOIZBJZDKZACHUQILCMZBJZDKUNUONOZUPUT
      LACPZBDUONQRUNUSVBDUNURVABUNURVAUNURDEZURVAJZDKZURVASUNVCVEVDDUOTUAUNVADK
      VGACDUBVAVFDVAURVAUOUQACDUCUDZUEUFUGURVADUHUIVHUJUKULUM $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph y z $.  $d ps y z $.
    $( Theorem *14.111 in [WhiteheadRussell] p. 184. $)
    iotasbc2 $p |- ( ( E! x ph /\ E! x ps ) -> ( [ ( iota x ph ) / y ]
        [ ( iota x ps ) / z ] ch <-> E. y E. z ( A. x ( ph <-> x = y ) /\
        A. x ( ps <-> x = z ) /\ ch ) ) ) $=
      ( weu cio wsbc cv wceq wb wal wa wex w3a iotasbc anbi2d 3anass exbii
      19.42v bitr2i syl6bb exbidv sylan9bb ) ADGCFBDHIZEADHIADJZEJKLDMZUFNZEOBD
      GZUHBUGFJKLDMZCPZFOZEOAUFDEQUJUIUMEUJUIUHUKCNZFOZNZUMUJUFUOUHBCDFQRUMUHUN
      NZFOUPULUQFUHUKCSTUHUNFUAUBUCUDUE $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d ph y $.  $d x y $.
    $( Theorem *14.12 in [WhiteheadRussell] p. 184. $)
    pm14.12 $p |- ( E! x ph -> A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) )
        $=
      ( weu wmo cv wsbc wa wceq wi wal eumo ax-17 mo3 sylib ) ABDABEAABCFZGHBFP
      IJCKBKABLABCACMNO $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d y x A $.  $d y B $.  $d ph y $.
    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122a $p |- ( A e. C -> ( A. x ( ph <-> x = A ) <-> ( A. x ( ph ->
      x = A ) /\ [ A / x ] ph ) ) ) $=
      ( wcel cv wceq wi wal wa wsbc wb sbc6g bicomd anbi2d albiim syl5bb ) CDEZ
      ABFCGZHBIZSAHBIZJTABCKZJASLBIRUAUBTRUBUAABCDMNOASBPQ $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122b $p |- ( A e. C -> ( ( A. x ( ph -> x = A ) /\ [ A / x ] ph ) <->
      ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( vy wcel cv wceq wi wal wsbc wex wb eqeq2 imbi2d albidv dfsbcq bibi1d
      imbi12d wa hba1 simpr ancr a4s impbid2 exbid sb5 syl5bb vtoclg pm5.32d )
      CDFABGZCHZIZBJZABCKZABLZAUKEGZHZIZBJZABUQKZUPMZIUNUOUPMZIECDUQCHZUTUNVBVC
      VDUSUMBVDURULAUQCUKNOPVDVAUOUPABUQCQRSUTURATZBLUPVAUTVEABUSBUAUTVEAURAUBU
      SAVEIBAURUCUDUEUFABEUGUHUIUJ $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122c $p |- ( A e. C -> ( A. x ( ph <-> x = A ) <->
                    ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( wcel cv wceq wb wal wi wsbc wa wex pm14.122a pm14.122b bitrd ) CDEABFCG
      ZHBIAQJBIZABCKLRABMLABCDNABCDOP $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)
  $}

  ${
    $d w A z $.  $d w B z $.
    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123a $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      [ A / z ] [ B / w ] ph ) ) ) $=
      ( wcel wa cv wceq wi wal wsbc wb 2sbc6g anbi2d 2albiim syl5bb ) DFHEGHIZA
      BJDKCJEKIZLCMBMZUAALCMBMZIUBACENBDNZIAUAOCMBMTUCUDUBABCDEFGPQAUABCRS $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123b $p |- ( ( A e. C /\ B e. D ) -> ( ( A. z A. w ( ph ->
      ( z = A /\ w = B ) ) /\ [ A / z ] [ B / w ] ph ) <->
      ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\ E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wi wal wsbc wex wb 2sbc5g adantr hba1 hba2 simpr ax-4
      a4s ancrd impbid2 exbid adantl bitr3d ex pm5.32d ) DFHEGHIZABJDKCJEKIZLZC
      MZBMZACENBDNZACOZBOZUKUOUPURPUKUOIULAIZCOZBOZUPURUKVAUPPUOABCDEFGQRUOVAUR
      PUKUOUTUQBUNBSUOUSACUMCBTUOUSAULAUAUOAULUNUMBUMCUBUCUDUEUFUFUGUHUIUJ $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123c $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wb wal wi wsbc wex pm14.123a pm14.123b bitrd ) DFHEGHIA
      BJDKCJEKIZLCMBMATNCMBMZACEOBDOIUAACPBPIABCDEFGQABCDEFGRS $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)
  $}

  ${
    $( Theorem *14.18 in [WhiteheadRussell] p. 189. $)
    pm14.18 $p |- ( E! x ph -> ( A. x ps -> [ ( iota x ph ) / x ] ps ) ) $=
      ( weu cio cvv wcel wal wsbc wi iotaexeu a4sbc syl ) ACDACEZFGBCHBCNIJACKB
      CNFLM $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.
    $( Theorem *14.2 in [WhiteheadRussell] p. 189. $)
    iota2 $p |- ( iota x x = y ) = y $=
      ( cv wceq wb cio iotaval biid mpg ) ACBCZDZKEKAFJDAKABGKHI $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    $( Theorem *14.202 in [WhiteheadRussell] p. 189.  A biconditional version
       of ~ iotaval . $)
    iotavalb $p |- ( E! x ph -> ( A. x ( ph <-> x = y ) <->
        ( iota x ph ) = y ) ) $=
      ( vz weu cv wceq wb wal cio iotaval wa wex wsbc iotasbc cvv wcel
      iotaexeu eqsbc3 syl bitr3d equequ2 bibi2d albidv biimpac 19.23aiv
      syl6bir impbid2 ) ABEZABFZCFZGZHZBIZABJZUKGZABCKUIUPAUJDFZGZHZBIZUQUKGZLZ
      DMZUNUIVADUONZVCUPAVABDOUIUOPQVDUPHABRDUOUKPSTUAVBUNDVAUTUNVAUSUMBVAURULA
      DCBUBUCUDUEUFUGUH $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.205 in [WhiteheadRussell] p. 190. $)
    iotasbc5 $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
        E. y ( y = ( iota x ph ) /\ ps ) ) ) $=
      ( weu cio cvv wcel wsbc cv wceq wa wex wb iotaexeu sbc5g syl ) ACEACFZGHB
      DRIDJRKBLDMNACOBDRGPQ $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d ph y $.  $d ps y $.  $d x y $.
    $( Theorem *14.23 in [WhiteheadRussell] p. 191.  (Moved to ~ iota4an in
       main set.mm and may be deleted by mathbox owner, ATS. --NM 7-Sep-2011.
       Note that ~ iota4an also has shorter proof.) $)
    euunianOLD $p |- ( E! x ( ph /\ ps )
             -> [ ( iota x ( ph /\ ps ) ) / x ] ph ) $=
      ( vy wa weu cv wceq wb wal wex cio wsbc df-eu wi bi2 alimi simpl imim2i
      sb2 3syl iotaval eqcomd dfsbcq syl mpbid 19.23aiv sylbi ) ABEZCFUICGDGZHZ
      IZCJZDKACUICLZMZUICDNUMUODUMACUJMZUOUMUKUIOZCJUKAOZCJUPULUQCUIUKPQUQURCUI
      AUKABRSQACDTUAUMUJUNHUPUOIUMUNUJUICDUBUCACUJUNUDUEUFUGUH $.
      $( [12-Jul-2011] $)
  $}

  ${
    $d z y x $.  $d z y ph $.
    $( Theorem *14.24 in [WhiteheadRussell] p. 191. $)
    pm14.24 $p |- ( E! x ph -> A. y ( [ y / x ] ph <-> y = ( iota x ph ) ) ) $=
      ( weu cv wsbc cio wceq wb wal hbeu1 hbs1 wa wi pm14.12 19.21bbi ancomsd
      expdimp sbequ2 com12 adantl impbid ex 19.21ad iotaval eqcomd syl6 dfsbcq
      iota4 syl5cbir 19.21aiv ) ABDZABCEZFZUMABGZHZICULUNUPULUNABEUMHZIZBJZUPUL
      UNURBABKABCLULUNURULUNMAUQULUNAUQULAUNUQULAUNMUQNBCABCOPQRUNUQANULUQUNAAB
      CSTUAUBUCUDUSUOUMABCUEUFUGUPUNABUOFULABUMUOUHABUIUJUBUK $.
      $( [1-Aug-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.242 in [WhiteheadRussell] p. 192. $)
    iotavalsb $p |- ( A. x ( ph <-> x = y ) -> ( [ y / z ] ps <->
        [ ( iota x ph ) / z ] ps ) ) $=
      ( cv wceq wb wal wex wsbc cio 19.8a weu wi df-eu iotavalb dfsbcq eqcoms
      syl6bi sylbir mpcom ) ACFDFZGHCIZDJZUDBEUCKBEACLZKHZUDDMUEACNZUDUGOACDPUH
      UDUFUCGUGACDQUGUCUFBEUCUFRSTUAUB $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Theorem *14.25 in [WhiteheadRussell] p. 192. $)
    sbiota1 $p |- ( E! x ph -> ( A. x ( ph -> ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( vy weu wi wal cio wsbc cv wceq wb wex df-eu biimpi iota4 iotaval
      eqcomd dfsbcq imbi12d a4sbim syl5bi com23 syl 19.23aiv sylc wa cvv wcel
      iotaexeu anbi12d imbi1d sban a4sbe sylbir vtoclg exp3a anc2li eupicka
      syl6 impbid ) ACEZABFCGZBCACHZIZVBACJDJZKLCGZDMZACVDIZVCVEFZVBVHACDNOACPZ
      VGVIVJFZDVGVFVDKZVLVGVDVFACDQRVMVCVIVEVMACVFIZBCVFIZFVIVEFVCVMVNVIVOVEACV
      FVDSZBCVFVDSZTABCDUAUBUCUDUEUFVBVEVBABUGZCMZUGVCVBVEVSVBVDUHUIZVIVEVSFACU
      JVKVTVIVEVSVNVOUGZVSFVIVEUGZVSFDVDUHVMWAWBVSVMVNVIVOVEVPVQUKULWAVRCVFIVSA
      BCDUMVRCDUNUOUPUQUFURABCUSUTVA $.
      $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.26 in [WhiteheadRussell] p. 192. $)
    eupickbi $p |- ( E! x ph -> ( E. x ( ph /\ ps ) <-> A. x ( ph -> ps ) ) )
        $=
      ( weu wa wex wi wal eupicka ex hba1 wb ancl simpl impbid1 a4s eubid
      biimpd euex syl6 com12 impbid ) ACDZABEZCFZABGZCHZUCUEUGABCIJUGUCUEUGUCUD
      CDZUEUGUCUHUGAUDCUFCKUFAUDLCUFAUDABMABNOPQRUDCSTUAUB $.
      $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem *14.26 in [WhiteheadRussell] p. 192. $)
    sbaniota $p |- ( E! x ph -> ( E. x ( ph /\ ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( weu wa wex wi wal cio wsbc eupickbi sbiota1 bitrd ) ACDABECFABGCHBCACIJ
      ABCKABCLM $.
      $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.271 in [WhiteheadRussell] p. 192. $)
    eubi $p |- ( A. x ( ph <-> ps ) -> ( E! x ph <-> E! x ps ) ) $=
      ( wb wal hba1 ax-4 eubid ) ABDZCEABCICFICGH $.
      $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem *14.272 in [WhiteheadRussell] p. 193. $)
    iotasbcq $p |- ( A. x ( ph <-> ps ) -> ( [ ( iota x ph ) / y ] ch <->
        [ ( iota x ps ) / y ] ch ) ) $=
      ( wb wal cio wceq wsbc iotabi dfsbcq syl ) ABFDGADHZBDHZICENJCEOJFABDKCEN
      OLM $.
      $( [11-Jul-2011] $)
  $}

