$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Principia Mathematica * 13 and * 14
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( One result of theorem *13.13 in [WhiteheadRussell] p. 178.  A note on the
     section - to make the theorems more usable, and because inequality is
     notation for set theory (it is not defined in the predicate calculus
     section), this section will use classes instead of sets. $)
  pm13.13a $p |- ( ( ph /\ x = A ) -> [ A / x ] ph ) $=
    ( cv wceq wsbc sbceq1a biimpac ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.13 in [WhiteheadRussell] p. 178 with different variable
     substitution. $)
  pm13.13b $p |- ( ( [ A / x ] ph /\ x = A ) -> ph ) $=
    ( cv wceq wsbc sbceq1a biimparc ) BDCEAABCFABCGH $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.14 in [WhiteheadRussell] p. 178. $)
  pm13.14 $p |- ( ( [ A / x ] ph /\ -. ph ) -> x =/= A ) $=
    ( wsbc wn cv wne wceq sbceq1a biimprcd necon3bd imp ) ABCDZAEBFZCGMANCNCHAM
    ABCIJKL $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.18 in [WhiteheadRussell] p. 178. $)
  pm13.18 $p |- ( ( A = B /\ A =/= C ) -> B =/= C ) $=
    ( wceq wne eqeq1 biimprd necon3d imp ) ABDZACEBCEJBCACJACDBCDABCFGHI $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.181 in [WhiteheadRussell] p. 178. $)
  pm13.181 $p |- ( ( A = B /\ B =/= C ) -> A =/= C ) $=
    ( wceq wne pm13.18 eqcom sylanb ) BADBCEACEABDBACFABGH $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  ${
    $d y A z $.  $d y B z $.
    $( Compare theorem *13.183 in [WhiteheadRussell] p. 178.  Only ` A ` is
       required to be a set. $)
    pm13.183 $p |- ( A e. C -> ( A = B <-> A. z ( z = A <-> z = B ) ) ) $=
      ( vy cv wceq wb wal eqeq1 eqeq2 bibi1d albidv 19.21aiv wsbc stdpc4 sbbi
      eqsb3 bibi2i equsb1 bi1 mpi sylbi syl impbii vtoclbg ) EFZCGZAFZUGGZUICGZ
      HZAIZBCGUIBGZUKHZAIEBDUGBCJUGBGZULUOAUPUJUNUKUGBUIKLMUHUMUHULAUGCUIKNUMUL
      AUGOZUHULAEPUQUJAUGOZUKAUGOZHZUHUJUKAEQUTURUHHZUHUSUHUREACRSVAURUHAETURUH
      UAUBUCUCUDUEUF $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d ph z $.
    $( Theorem *13.192 in [WhiteheadRussell] p. 179. $)
    pm13.192 $p |- ( A e. _V -> ( E. y ( A. x ( x = A <-> x = y ) /\ ph ) <->
      [ A / y ] ph ) ) $=
      ( cvv wcel wsbc cv wceq wa wex wb wal sbc5g wi bi2 19.20i ax-17 eqeq1
      equsal biimpi syl eqeq2 eqcoms 19.21aiv impbii anbi1i exbii syl6rbbr ) DE
      FACDGCHZDIZAJZCKBHZDIZUMUJIZLZBMZAJZCKACDENURULCUQUKAUQUKUQUOUNOZBMZUKUPU
      SBUNUOPQUTUKUNUKBCUKBRUMUJDSTUAUBUKUPBUPDUJDUJUMUCUDUEUFUGUHUI $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  $( Theorem *13.193 in [WhiteheadRussell] p. 179. $)
  pm13.193 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ x = y ) ) $=
    ( weq wsb sbequ12 pm5.32ri ) BCDAABCEABCFG $.
    $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  $( Theorem *13.194 in [WhiteheadRussell] p. 179. $)
  pm13.194 $p |- ( ( ph /\ x = y ) <-> ( [ y / x ] ph /\ ph /\ x = y ) ) $=
    ( cv wceq wa wsbc w3a pm13.13a pm3.26 pm3.27 3jca 3simpc impbii ) ABDCDZEZF
    ZABOGZAPHQRAPABOIAPJAPKLRAPMN $.
    $( [16-Jun-2011] $) $( [3-Jun-2011] $)

  ${
    $d y A $.
    pm13.195.1 $e |- A e. _V $.
    $( Theorem *13.195 in [WhiteheadRussell] p. 179.  This theorem is very
       similar to sbc5. $)
    pm13.195 $p |- ( E. y ( y = A /\ ph ) <-> [ A / y ] ph ) $=
      ( wsbc cv wceq wa wex sbc5 bicomi ) ABCEBFCGAHBIABCDJK $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *13.196 in [WhiteheadRussell] p. 179.  The only difference is
       the position of the substituted variable. $)
    pm13.196a $p |- ( -. ph <-> A. y ( [ y / x ] ph -> y =/= x ) ) $=
      ( wn cv wceq wsbc wa wex wi wal wne sbelx sb56 sbn imbi2i con2b df-ne
      3bitr2ri 3bitri albii ) ADZCEZBEZFZUBBUCGZHCIUEUFJZCKABUCGZUCUDLZJZCKUBCB
      MUFCBNUGUJCUGUEUHDZJUHUEDZJUJUFUKUEABCOPUEUHQULUIUHUIULUIULUCUDRZUMUMSPTU
      AT $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.21 in [WhiteheadRussell] p. 179. $)
    2sbc6g $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ( z = A /\
      w = B ) -> ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wi wal wsbc wb eqeq2 anbi2d imbi1d 2albidv ax-17
      dfsbcq sbbid bibi12d anbi1d 2sb6 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZE
      LZMZANZCOBOZACEPZBDPZQZUKHKZLZUMIKZLZMZANZCOBOZACVCPZBVAPZQVBUNMZANZCOBOZ
      URBVAPZQUTIHEDGFVCELZVGVLVIVMVNVFVKBCVNVEVJAVNVDUNVBVCEUMRSTUAVNVHURBHVNB
      UBACVCEUCUDUEVADLZVLUQVMUSVOVKUPBCVOVJUOAVOVBULUNVADUKRUFTUAURBVADUCUEVIV
      GABCHIUGUHUIUJ $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.22 in [WhiteheadRussell] p. 179. $)
    2sbc5g $p |- ( ( A e. C /\ B e. D ) -> ( E. z E. w ( ( z = A /\ w = B )
      /\ ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wex wsbc wb eqeq2 anbi2d anbi1d 2exbidv ax-17
      dfsbcq sbbid bibi12d 2sb5 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZELZMZAMZ
      CNBNZACEOZBDOZPZUIHKZLZUKIKZLZMZAMZCNBNZACVAOZBUSOZPUTULMZAMZCNBNZUPBUSOZ
      PURIHEDGFVAELZVEVJVGVKVLVDVIBCVLVCVHAVLVBULUTVAEUKQRSTVLVFUPBHVLBUAACVAEU
      BUCUDUSDLZVJUOVKUQVMVIUNBCVMVHUMAVMUTUJULUSDUIQSSTUPBUSDUBUDVGVEABCHIUEUF
      UGUH $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y B $.  $d y A $.
    $( Substitution applied to an atomic wff. $)
    eqsbc3 $p |- ( A e. C -> ( [ A / x ] x = B <-> A = B ) ) $=
      ( vy cv wceq wsbc dfsbcq eqeq1 eqsb3 vtoclbg ) AFCGZAEFZHNCGMABHBCGEBDMAN
      BINBCJEACKL $.
      $( [29-Jun-2011] $) $( [29-Jun-2011] $)
  $}

  ${
    $d x B $.  $d x A $.
    $( Not in Principia Mathematica but is useful for proving the theorems in
       this section.  It is the set theory version of ~ sbeqal1 . $)
    sbceqal $p |- ( A e. C -> ( A. x ( x = A -> x = B ) -> A = B ) ) $=
      ( wcel cv wceq wi wal wsbc a4sbc sbcimg wb eqid eqeq1 sbcieg mpbiri
      pm5.5 syl eqsbc3 3bitrd sylibd ) BDEZAFZBGZUDCGZHZAIUGABJZBCGZUGABDKUCUHU
      EABJZUFABJZHZUKUIUEUFABDLUCUJULUKMUCUJBBGZBNUEUMABDUDBBOPQUJUKRSABCDTUAUB
      $.
      $( [28-Jun-2011] $) $( [28-Jun-2011] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Theorem *14.121 in [WhiteheadRussell] p. 185. $)
    sbeqalb $p |- ( A e. C -> ( ( A. x ( ph <-> x = A ) /\ A. x ( ph <->
      x = B ) ) -> A = B ) ) $=
      ( wcel cv wceq wi wal wb wa sbceqal bibi1 biimpa bi1 syl 19.20ian syl5 )
      CEFBGZCHZTDHZIZBJCDHAUAKZBJAUBKZBJLBCDEMUDUEUCBUDUELUAUBKZUCUDUEUFAUAUBNO
      UAUBPQRS $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)
  $}

  ${
    $d ph z $.  $d x y z $.
    $( Theorem 8.19 in [Quine] p. 57.  This theorem is the fundamental property
       of iota. $)
    iotaval $p |- ( A. x ( ph <-> x = y ) -> y = ( iota x ph ) ) $=
      ( vz cv wceq wb wal cab cuni ciota cvv wcel wa wi visset sbeqalb equcomi
      syl6 ax-mp ex equequ2 eqcoms bibi2d biimpd 19.20dv com12 impbid 19.21aiv
      uniabio syl dfiota2 syl5req ) ABEZCEZFZGZBHZAUNDEZFZGZBHZDIJZUOABKURVBUSU
      OFZGZDHVCUOFURVEDURVBVDURVBVDUOLMZURVBNZVDOCPVFVGUOUSFVDABUOUSLQCDRSTUAVD
      URVBVDUQVABVDUQVAVDUPUTAUPUTGUOUSCDBUBUCUDUEUFUGUHUIVBDCUJUKABDULUM $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Equivalence between two different forms of ` iota ` . $)
    iotaequ $p |- ( E! x ph -> U. { x | ph } = ( iota x ph ) ) $=
      ( vy weu cv wceq wb wal wex cab cuni ciota df-eu uniabio iotaval eqtrd
      19.23aiv sylbi ) ABDABECEZFGBHZCIABJKZABLZFZABCMTUCCTUASUBABCNABCOPQR $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Equivalence between two different forms of ` iota ` . $)
    iotain $p |- ( E! x ph -> |^| { x | ph } = ( iota x ph ) ) $=
      ( vy weu cv wceq wb wal wex cab cint ciota df-eu csn hba1 ax4 abbid
      df-sn syl6eqr inteqd visset intsn a1i iotaval 3eqtrd 19.23aiv sylbi ) ABD
      ABECEZFZGZBHZCIABJZKZABLZFZABCMUKUOCUKUMUHNZKZUHUNUKULUPUKULUIBJUPUKAUIBU
      JBOUJBPQBUHRSTUQUHFUKUHCUAUBUCABCUDUEUFUG $.
      $( [15-Jul-2011] $) $( [15-Jul-2011] $)
  $}

  ${
    $d z y ph $.  $d z y x $.
    $( The iota class exists.  This theorem does not require ~ ax-nul for its
       proof. $)
    iotaexeu $p |- ( E! x ph -> ( iota x ph ) e. _V ) $=
      ( vy cv wceq wb wal wex ciota weu cvv wcel iotaval 19.22i df-eu isset
      3imtr4i ) ABDCDZEFBGZCHRABIZEZCHABJTKLSUACABCMNABCOCTPQ $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d y ph $.  $d x y $.
    $( Theorem 8.22 in [Quine] p. 57.  This theorem is the result if there
       isn't exactly one ` x ` that satisfies ` ph ` . $)
    iotanul $p |- ( -. E! x ph -> (/) = ( iota x ph ) ) $=
      ( vy weu wn cv wceq wb wal wex c0 ciota df-eu notbii cab cuni alnex ax-1
      eqidd impbid1 con2bid 19.20i eq2ab sylibr dfnul2 syl6eqr sylbir unieqd
      uni0 syl6eq dfiota2 syl5req sylbi ) ABDZEABFCFZGHBIZCJZEZKABLZGUNUQABCMNU
      RUPCOZPZKUSURVAKPKURUTKURUPEZCIZUTKGUPCQVCUTUOUOGZEZCOZKVCUPVEHZCIUTVFGVB
      VGCVBVDUPVBVDVBVBVDRVBUOSTUAUBUPVECUCUDCUEUFUGUHUIUJABCUKULUM $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem 8.23 in [Quine] p. 58.  This theorem proves the existence of the
       ` iota ` class under our definition. $)
    iotaex $p |- ( iota x ph ) e. _V $=
      ( weu ciota cvv wcel iotaexeu wn c0 wceq iotanul 0ex eleq1 mpbii syl
      pm2.61i ) ABCZABDZEFZABGQHIRJZSABKTIEFSLIREMNOP $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph y z $.
    $( Hypothesis builder for the ` iota ` class. $)
    hbiota $p |- ( y e. ( iota x ph ) -> A. x y e. ( iota x ph ) ) $=
      ( vz cv wceq wb wal cab cuni wcel ciota hba1 hbab hbuni dfiota2 eleq2i
      albii 3imtr4i ) CEZABEDEFGZBHZDIZJZKZUEBHTABLZKZUGBHBCUCUBBDCUABMNOUFUDTA
      BDPQZUGUEBUHRS $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Definition *14.01 in [WhiteheadRussell] p. 184.  In Principia
       Mathematica, Russell and Whitehead define ` iota ` in terms of a
       function of ` ( iota x ph ) ` .  Their definition differs in that a
       function of ` ( iota x ph ) ` evaluates to "false" when there isn't a
       single ` x ` that satisfies ` ph ` . $)
    iotasbc $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
         E. y ( A. x ( ph <-> x = y ) /\ ps ) ) ) $=
      ( weu ciota wsbc cv wceq wa wex wb wal cvv wcel iotaexeu sbc5g syl wi
      eueq sylib df-eu iotaval ancri 19.22i sylbi jca eupick impbid1 anbi1d
      exbidv bitrd ) ACEZBDACFZGZDHZUNIZBJZDKZACHUPILCMZBJZDKUMUNNOZUOUSLACPZBD
      UNNQRUMURVADUMUQUTBUMUQUTUMUQDEZUQUTJZDKZJUQUTSUMVDVFUMVBVDVCDUNTUAUMUTDK
      VFACDUBUTVEDUTUQACDUCZUDUEUFUGUQUTDUHRVGUIUJUKUL $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph y z $.  $d ps y z $.
    $( Theorem *14.111 in [WhiteheadRussell] p. 184. $)
    iotasbc2 $p |- ( ( E! x ph /\ E! x ps ) -> ( [ ( iota x ph ) / y ]
        [ ( iota x ps ) / z ] ch <-> E. y E. z ( A. x ( ph <-> x = y ) /\
        A. x ( ps <-> x = z ) /\ ch ) ) ) $=
      ( weu ciota wsbc cv wceq wb wal wa wex w3a iotasbc anbi2d 3anass exbii
      19.42v bitr2i syl6bb exbidv sylan9bb ) ADGCFBDHIZEADHIADJZEJKLDMZUFNZEOBD
      GZUHBUGFJKLDMZCPZFOZEOAUFDEQUJUIUMEUJUIUHUKCNZFOZNZUMUJUFUOUHBCDFQRUMUHUN
      NZFOUPULUQFUHUKCSTUHUNFUAUBUCUDUE $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}


  ${
    $d ph y $.  $d x y $.
    $( Theorem *14.12 in [WhiteheadRussell] p. 184. $)
    pm14.12 $p |- ( E! x ph -> A. x A. y ( ( ph /\ [ y / x ] ph ) -> x = y ) )
        $=
      ( weu wmo cv wsbc wa wceq wi wal eumo ax-17 mo3 sylib ) ABDABEAABCFZGHBFP
      IJCKBKABLABCACMNO $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d y x A $.  $d y B $.  $d ph y $.
    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122a $p |- ( A e. C -> ( A. x ( ph <-> x = A ) <-> ( A. x ( ph ->
      x = A ) /\ [ A / x ] ph ) ) ) $=
      ( wcel cv wceq wi wal wa wsbc wb sbc6g bicomd anbi2d albi syl5bb ) CDEZAB
      FCGZHBIZSAHBIZJTABCKZJASLBIRUAUBTRUBUAABCDMNOASBPQ $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122b $p |- ( A e. C -> ( ( A. x ( ph -> x = A ) /\ [ A / x ] ph ) <->
      ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( vy wcel cv wceq wi wal wsbc wex wb eqeq2 imbi2d albidv dfsbcq bibi1d
      imbi12d wa hba1 pm3.27 ancr a4s impbid2 exbid sb5 syl5bb vtoclg pm5.32d
      ) CDFABGZCHZIZBJZABCKZABLZAUKEGZHZIZBJZABUQKZUPMZIUNUOUPMZIECDUQCHZUTUNVB
      VCVDUSUMBVDURULAUQCUKNOPVDVAUOUPABUQCQRSUTURATZBLUPVAUTVEABUSBUAUTVEAURAU
      BUSAVEIBAURUCUDUEUFABEUGUHUIUJ $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.122 in [WhiteheadRussell] p. 185. $)
    pm14.122c $p |- ( A e. C -> ( A. x ( ph <-> x = A ) <->
                    ( A. x ( ph -> x = A ) /\ E. x ph ) ) ) $=
      ( wcel cv wceq wb wal wi wsbc wa wex pm14.122a pm14.122b bitrd ) CDEABFCG
      ZHBIAQJBIZABCKLRABMLABCDNABCDOP $.
      $( [28-Jun-2011] $) $( [9-Jun-2011] $)
  $}

  ${
    $d w A z $.  $d w B z $.
    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123a $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      [ A / z ] [ B / w ] ph ) ) ) $=
      ( wcel wa cv wceq wi wal wsbc wb 2sbc6g anbi2d 2albi syl5bb ) DFHEGHIZABJ
      DKCJEKIZLCMBMZUAALCMBMZIUBACENBDNZIAUAOCMBMTUCUDUBABCDEFGPQAUABCRS $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123b $p |- ( ( A e. C /\ B e. D ) -> ( ( A. z A. w ( ph ->
      ( z = A /\ w = B ) ) /\ [ A / z ] [ B / w ] ph ) <->
      ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\ E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wi wal wsbc wex wb 2sbc5g adantr hba1 hba2 pm3.27 ax-4
      a4s ancrd impbid2 exbid adantl bitr3d ex pm5.32d ) DFHEGHIZABJDKCJEKIZLZC
      MZBMZACENBDNZACOZBOZUKUOUPURPUKUOIULAIZCOZBOZUPURUKVAUPPUOABCDEFGQRUOVAUR
      PUKUOUTUQBUNBSUOUSACUMCBTUOUSAULAUAUOAULUNUMBUMCUBUCUDUEUFUFUGUHUIUJ $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)

    $( Theorem *14.123 in [WhiteheadRussell] p. 185. $)
    pm14.123c $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ph <->
      ( z = A /\ w = B ) ) <-> ( A. z A. w ( ph -> ( z = A /\ w = B ) ) /\
      E. z E. w ph ) ) ) $=
      ( wcel wa cv wceq wb wal wi wsbc wex pm14.123a pm14.123b bitrd ) DFHEGHIA
      BJDKCJEKIZLCMBMATNCMBMZACEOBDOIUAACPBPIABCDEFGQABCDEFGRS $.
      $( [29-Jun-2011] $) $( [9-Jun-2011] $)
  $}

  ${
    $( Theorem *14.18 in [WhiteheadRussell] p. 189. $)
    pm14.18 $p |- ( E! x ph -> ( A. x ps -> [ ( iota x ph ) / x ] ps ) ) $=
      ( weu ciota cvv wcel wal wsbc wi iotaexeu a4sbc syl ) ACDACEZFGBCHBCNIJAC
      KBCNFLM $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.
    $( Theorem *14.2 in [WhiteheadRussell] p. 189. $)
    iota2 $p |- ( iota x x = y ) = y $=
      ( cv wceq wb ciota wal iotaval eqcomd biid mpg ) ACBCZDZMEZMAFZLDANAGLOMA
      BHIMJK $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y z $.  $d ph z $.
    $( Theorem *14.202 in [WhiteheadRussell] p. 189.  A biconditional version
       of ~ iotaval . $)
    iotavalb $p |- ( E! x ph -> ( A. x ( ph <-> x = y ) <->
        ( iota x ph ) = y ) ) $=
      ( vz weu cv wceq wb wal ciota iotaval eqcomd wa wex wsbc iotasbc cvv
      wcel iotaexeu eqsbc3 syl bitr3d equequ2 bibi2d albidv biimpac 19.23aiv
      syl6bir impbid2 ) ABEZABFZCFZGZHZBIZABJZULGZUOULUPABCKLUJUQAUKDFZGZHZBIZU
      RULGZMZDNZUOUJVBDUPOZVDUQAVBBDPUJUPQRVEUQHABSDUPULQTUAUBVCUODVBVAUOVBUTUN
      BVBUSUMADCBUCUDUEUFUGUHUI $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.205 in [WhiteheadRussell] p. 190. $)
    iotasbc5 $p |- ( E! x ph -> ( [ ( iota x ph ) / y ] ps <->
        E. y ( y = ( iota x ph ) /\ ps ) ) ) $=
      ( weu ciota cvv wcel wsbc cv wceq wa wex wb iotaexeu sbc5g syl ) ACEACFZG
      HBDRIDJRKBLDMNACOBDRGPQ $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d ph y $.  $d x y $.
    $( Theorem *14.22 in [WhiteheadRussell] p. 190. $)
    iota3 $p |- ( E! x ph -> [ ( iota x ph ) / x ] ph ) $=
      ( vy weu cv wceq wb wal wex ciota wsbc df-eu wi bi2 19.20i sb2 syl
      iotaval dfsbcq mpbid 19.23aiv sylbi ) ABDABECEZFZGZBHZCIABABJZKZABCLUFUHC
      UFABUCKZUHUFUDAMZBHUIUEUJBAUDNOABCPQUFUCUGFUIUHGABCRABUCUGSQTUAUB $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d ph y $.  $d ps y $.  $d x y $.
    $( Theorem *14.23 in [WhiteheadRussell] p. 191. $)
    euunian $p |- ( E! x ( ph /\ ps ) -> [ ( iota x ( ph /\ ps ) ) / x ] ph )
        $=
      ( vy wa weu cv wceq wb wal wex ciota wsbc df-eu wi bi2 19.20i pm3.26
      imim2i sb2 3syl iotaval dfsbcq syl mpbid 19.23aiv sylbi ) ABEZCFUHCGDGZHZ
      IZCJZDKACUHCLZMZUHCDNULUNDULACUIMZUNULUJUHOZCJUJAOZCJUOUKUPCUHUJPQUPUQCUH
      AUJABRSQACDTUAULUIUMHUOUNIUHCDUBACUIUMUCUDUEUFUG $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d z y x $.  $d z y ph $.
    $( Theorem *14.24 in [WhiteheadRussell] p. 191. $)
    pm14.24 $p |- ( E! x ph -> A. y ( [ y / x ] ph <-> y = ( iota x ph ) ) ) $=
      ( weu cv wsbc ciota wceq wb wal hbeu1 hbs1 wa wi pm14.12 19.21bi ancomsd
      exp3a sbequ2 com12 a1i impbidd 19.21ad iotaval syl6 iota3 dfsbcq
      biimprcd syl impbid 19.21aiv ) ABDZABCEZFZUMABGZHZICULUNUPULUNABEUMHZIZBJ
      UPULUNURBABKABCLULUNAUQULUNAUQULAUNUQULAUNMUQNZCULUSCJBABCOPPQRUNUQANNULU
      QUNAABCSTUAUBUCABCUDUEULABUOFZUPUNNABUFUPUNUTABUMUOUGUHUIUJUK $.
      $( [1-Aug-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *14.242 in [WhiteheadRussell] p. 192. $)
    iotavalsb $p |- ( A. x ( ph <-> x = y ) -> ( [ y / z ] ps <->
        [ ( iota x ph ) / z ] ps ) ) $=
      ( cv wceq wb wal wex wsbc ciota 19.8a weu wi df-eu iotavalb dfsbcq
      eqcoms syl6bi sylbir mpcom ) ACFDFZGHCIZDJZUDBEUCKBEACLZKHZUDDMUEACNZUDUG
      OACDPUHUDUFUCGUGACDQUGUCUFBEUCUFRSTUAUB $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps y $.
    $( Theorem *14.25 in [WhiteheadRussell] p. 192. $)
    sbiota1 $p |- ( E! x ph -> ( A. x ( ph -> ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( vy weu wi wal ciota wsbc cv wceq wb wex iotaval dfsbcq imbi12d a4sbim
      syl5bi com23 syl 19.23aiv imp df-eu biimpi iota3 sylanc wa cvv wcel
      anbi12d imbi1d sban a4sbe sylbir vtoclg exp3a iotaexeu id jctild eupicka
      syl6 impbid ) ACEZABFCGZBCACHZIZACJDJZKLCGZDMZACVEIZVDVFFZVCVIVJVKVHVJVKF
      ZDVHVGVEKZVLACDNVMVDVJVFVMACVGIZBCVGIZFVJVFFVDVMVNVJVOVFACVGVEOZBCVGVEOZP
      ABCDQRSTUAUBVCVIACDUCUDACUEZUFVCVFVCABUGZCMZUGVDVCVFVTVCVEUHUIZVJVFVTFZVC
      WAVJWBWAVJVFVTVNVOUGZVTFVJVFUGZVTFDVEUHVMWCWDVTVMVNVJVOVFVPVQUJUKWCVSCVGI
      VTABCDULVSCDUMUNUOUPUBACUQVRUFVCURUSABCUTVAVB $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.26 in [WhiteheadRussell] p. 192. $)
    eupickbi $p |- ( E! x ph -> ( E. x ( ph /\ ps ) <-> A. x ( ph -> ps ) ) )
        $=
      ( weu wa wex wi wal eupicka ex hba1 wb ancl pm3.26 impbid1 a4s eubid
      biimpd euex syl6 com12 impbid ) ACDZABEZCFZABGZCHZUCUEUGABCIJUGUCUEUGUCUD
      CDZUEUGUCUHUGAUDCUFCKUFAUDLCUFAUDABMABNOPQRUDCSTUAUB $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem *14.26 in [WhiteheadRussell] p. 192. $)
    sbaniota $p |- ( E! x ph -> ( E. x ( ph /\ ps ) <-> [ ( iota x ph ) / x ]
        ps ) ) $=
      ( weu wa wex wi wal ciota wsbc eupickbi sbiota1 bitrd ) ACDABECFABGCHBCAC
      IJABCKABCLM $.
      $( [12-Jul-2011] $) $( [12-Jul-2011] $)
  $}

  ${
    $( Theorem *14.271 in [WhiteheadRussell] p. 192. $)
    eubi $p |- ( A. x ( ph <-> ps ) -> ( E! x ph <-> E! x ps ) ) $=
      ( wb wal hba1 ax-4 eubid ) ABDZCEABCICFICGH $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

  ${
    $( Theorem *14.272 in [WhiteheadRussell] p. 193. $)
    iotasbcq $p |- ( A. x ( ph <-> ps ) -> ( [ ( iota x ph ) / y ] ch <->
        [ ( iota x ps ) / y ] ch ) ) $=
      ( wb wal ciota wceq wsbc iotabi dfsbcq syl ) ABFDGADHZBDHZICENJCEOJFABDKC
      ENOLM $.
      $( [11-Jul-2011] $) $( [11-Jul-2011] $)
  $}

