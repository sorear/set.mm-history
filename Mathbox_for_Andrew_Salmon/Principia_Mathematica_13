$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Principia Mathematica * 13
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

    $( One result of theorem *13.13 in [WhiteheadRussell] p. 178.  A note on
       the section - to make the theorems more usable, and because inequality
       is notation for set theory (it is not defined in the predicate calculus
       section), this section will use classes instead of sets. $)
    pm13.13a $p |- ( ( ph /\ x = A ) -> [ A / x ] ph ) $=
      ( cv wceq wsbc sbceq1a biimpac ) BDCEAABCFABCGH $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( Theorem *13.13 in [WhiteheadRussell] p. 178 with different variable
       substitution. $)
    pm13.13b $p |- ( ( [ A / x ] ph /\ x = A ) -> ph ) $=
      ( cv wceq wsbc sbceq1a biimparc ) BDCEAABCFABCGH $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( Theorem *13.14 in [WhiteheadRussell] p. 178. $)
    pm13.14 $p |- ( ( [ A / x ] ph /\ -. ph ) -> x =/= A ) $=
      ( wsbc wn cv wne wceq sbceq1a biimprcd necon3bd imp ) ABCDZAEBFZCGMANCNCH
      AMABCIJKL $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( Theorem *13.18 in [WhiteheadRussell] p. 178. $)
    pm13.18 $p |- ( ( A = B /\ A =/= C ) -> B =/= C ) $=
      ( wceq wne eqeq1 biimprd necon3d imp ) ABDZACEBCEJBCACJACDBCDABCFGHI $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( Theorem *13.181 in [WhiteheadRussell] p. 178. $)
    pm13.181 $p |- ( ( A = B /\ B =/= C ) -> A =/= C ) $=
      ( wceq wne pm13.18 eqcom sylanb ) BADBCEACEABDBACFABGH $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

  ${
    $d y A z $.  $d y B z $.
    $( Compare theorem *13.183 in [WhiteheadRussell] p. 178.  Only ` A ` is
       required to be a set. $)
    pm13.183 $p |- ( A e. C -> ( A = B <-> A. z ( z = A <-> z = B ) ) ) $=
      ( vy cv wceq wb wal eqeq1 eqeq2 bibi1d albidv 19.21aiv wsbc stdpc4 sbbi 
      eqsb3 bibi2i equsb1 bi1 mpi sylbi syl impbii vtoclbg ) EFZCGZAFZUGGZUICGZ
      HZAIZBCGUIBGZUKHZAIEBDUGBCJUGBGZULUOAUPUJUNUKUGBUIKLMUHUMUHULAUGCUIKNUMUL
      AUGOZUHULAEPUQUJAUGOZUKAUGOZHZUHUJUKAEQUTURUHHZUHUSUHUREACRSVAURUHAETURUH
      UAUBUCUCUDUEUF $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d y A $.
    pm13.191.1 $e |- A e. _V $.
    $( Theorem *13.191 in [WhiteheadRussell] p. 178. Similar to sbc6. $)
    pm13.191 $p |- ( A. y ( y = A -> ph ) <-> [ A / y ] ph ) $=
      ( wsbc cv wceq wi wal sbc6 bicomi ) ABCEBFCGAHBIABCDJK $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y z A $.  $d ph z $.
    $( Theorem *13.192 in [WhiteheadRussell] p. 179. $)
    pm13.192 $p |- ( A e. _V -> ( E. y ( A. x ( x = A <-> x = y ) /\ ph ) <->
      [ A / y ] ph ) ) $=
      ( cvv wcel wsbc cv wceq wa wex wb wal sbc5g visset pm13.183 pm10.32 
      syl6bb ax-mp anbi1i exbii syl6rbb ) DEFACDGCHZDIZAJZCKBHZDIZUFUCIZLBMZAJZ
      CKACDENUEUJCUDUIAUCEFZUDUILCOUKUDUHUGLBMUIBUCDEPUHUGBQRSTUAUB $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

    $( Theorem *13.193 in [WhiteheadRussell] p. 179. $)
    pm13.193 $p |- ( A. x x = y <-> A. y x = y ) $=
      ( weq wal ax-10 impbii equcom albii bitri ) ABCZADZBACZBDZJBDKMABEBAEFLJB
      BAGHI $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( Theorem *13.194 in [WhiteheadRussell] p. 179. $)
    pm13.194 $p |- ( A. x x = y <-> A. x A. y x = y ) $=
      ( weq wal hbae ax-4 impbii alcom bitri ) ABCZADZKBDZJBDADKLABBEKBFGJBAHI
      $.
      $( [16-Jun-2011] $) $( [3-Jun-2011] $)

  ${
    $d y A $.  
    pm13.195.1 $e |- A e. _V $.
    $( Theorem *13.195 in [WhiteheadRussell] p. 179. This theorem is very
       similar to sbc5. $)
    pm13.195 $p |- ( E. y ( y = A /\ ph ) <-> [ A / y ] ph ) $=
      ( wsbc cv wceq wa wex sbc5 bicomi ) ABCEBFCGAHBIABCDJK $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    $( Theorem *13.196 in [WhiteheadRussell] p. 179. The only difference is
       the position of the substituted variable. $)
    pm13.196a $p |- ( -. ph <-> A. y ( [ y / x ] ph -> y =/= x ) ) $=
      ( wn weq wsb wa wex wi wal cv wne sbelx sb56 sbn imbi2i con2b df-ne
      3bitr2ri 3bitri albii ) ADZCBEZUBBCFZGCHUCUDIZCJABCFZCKZBKZLZIZCJUBCBMUDC
      BNUEUJCUEUCUFDZIUFUCDZIUJUDUKUCABCOPUCUFQULUIUFUIULUIULUGUHRZUMUMSPTUAT
      $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.21 in [WhiteheadRussell] p. 179. $)
    pm13.21 $p |- ( ( A e. C /\ B e. D ) -> ( A. z A. w ( ( z = A /\
      w = B ) -> ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wi wal wsbc wb eqeq2 anbi2d imbi1d 2albidv ax-17 
      dfsbcq sbbid bibi12d anbi1d 2sb6 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZE
      LZMZANZCOBOZACEPZBDPZQZUKHKZLZUMIKZLZMZANZCOBOZACVCPZBVAPZQVBUNMZANZCOBOZ
      URBVAPZQUTIHEDGFVCELZVGVLVIVMVNVFVKBCVNVEVJAVNVDUNVBVCEUMRSTUAVNVHURBHVNB
      UBACVCEUCUDUEVADLZVLUQVMUSVOVKUPBCVOVJUOAVOVBULUNVADUKRUFTUAURBVADUCUEVIV
      GABCHIUGUHUIUJ $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d x y ph $.
    $( Theorem *13.22 in [WhiteheadRussell] p. 179. $)
    pm13.22 $p |- ( ( A e. C /\ B e. D ) -> ( E. z E. w ( ( z = A /\ w = B )
      /\ ph ) <-> [ A / z ] [ B / w ] ph ) ) $=
      ( vx vy wcel cv wceq wa wex wsbc wb eqeq2 anbi2d anbi1d 2exbidv ax-17 
      dfsbcq sbbid bibi12d 2sb5 bicomi vtocl2g ancoms ) EGJDFJBKZDLZCKZELZMZAMZ
      CNBNZACEOZBDOZPZUIHKZLZUKIKZLZMZAMZCNBNZACVAOZBUSOZPUTULMZAMZCNBNZUPBUSOZ
      PURIHEDGFVAELZVEVJVGVKVLVDVIBCVLVCVHAVLVBULUTVAEUKQRSTVLVFUPBHVLBUAACVAEU
      BUCUDUSDLZVJUOVKUQVMVIUNBCVMVHUMAVMUTUJULUSDUIQSSTUPBUSDUBUDVGVEABCHIUEUF
      UGUH $.
      $( [28-Jun-2011] $) $( [3-Jun-2011] $)
  $}

