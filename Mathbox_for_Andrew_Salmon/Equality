$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
               Equality
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)
  ${
    $d x z $.
    $( If ` x = y ` always implies ` x = z `, then ` y = z ` is true. $)
    sbeqal1 $p |- ( A. x ( x = y -> x = z ) -> y = z ) $=
      ( weq wi wal wsb sb2 equsb3 sylib ) ABDACDZEAFKABGBCDKABHBACIJ $.
      $( [2-Jun-2011] $) $( [2-Jun-2011] $)
  $}

  ${
    $d x z $.
    sbeqal1i.1 $e |- ( x = y -> x = z ) $.
    $( Suppose you know ` x = y ` implies ` x = z `, assuming ` x ` and ` z `
       are distinct.  Then, ` y = z `. $)
    sbeqal1i $p |- y = z $=
      ( weq wi sbeqal1 mpg ) ABEACEFBCEAABCGDH $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)

    $( If ` x = y ` implies ` x = z `, then we can infer ` z = y `. $)
    sbeqal2i $p |- z = y $=
      ( cv sbeqal1i eqcomi ) BECEABCDFG $.
      $( [3-Jun-2011] $) $( [3-Jun-2011] $)
  $}

  ${
    $d y z $.    $d x z $.
    $( When both ` x ` and ` z ` and ` y ` and ` z ` are both distinct, then
       the converse of sbeqal1 holds as well. $)
    sbeqalbi $p |- ( x = y <-> A. z ( z = x -> z = y ) ) $=
      ( weq wi wal equtrr 19.21aiv sbeqal1 impbii ) ABDZCADCBDEZCFKLCABCGHCABIJ
      $.
      $( [2-Jun-2011] $) $( [2-Jun-2011] $)
  $}

