$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              The Predecessor Class
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $c Pred $.
  $( The predecessors symbol. $)
  cpred $a class Pred ( R , A , X ) $.

  $( Define the predecessor class of a relationship.  This is the class of all
     elements ` y ` of ` A ` such that ` y R X ` (see ~ elpred ) . $)
  df-pred $a |- Pred ( R , A , X ) = ( A i^i ( `' R " { X } ) ) $.

  $( Equality theorem for the predecessor class. $)
  predeq1 $p |- ( R = S -> Pred ( R , A , X ) = Pred ( S , A , X ) ) $=
    ( wceq ccnv csn cima cin cpred cnveq imaeq1d ineq2d df-pred 3eqtr4g ) BCEZA
    BFZDGZHZIACFZRHZIABDJACDJPSUAAPQTRBCKLMABDNACDNO $.
    $( [2-Feb-2011] $)

  $( Equality theorem for the predecessor class. $)
  predeq2 $p |- ( A = B -> Pred ( R , A , X ) = Pred ( R , B , X ) ) $=
    ( wceq ccnv csn cima cin cpred ineq1 df-pred 3eqtr4g ) ABEACFDGHZIBNIACDJBC
    DJABNKACDLBCDLM $.
    $( [2-Feb-2011] $)

  $( Equality theorem for the predecessor class. $)
  predeq3 $p |- ( X = Y -> Pred ( R , A , X ) = Pred ( R , A , Y ) ) $=
    ( wceq ccnv csn cima cin cpred sneq imaeq2d ineq2d df-pred 3eqtr4g ) CDEZAB
    FZCGZHZIAQDGZHZIABCJABDJPSUAAPRTQCDKLMABCNABDNO $.
    $( [2-Feb-2011] $)

  $( If ` A ` is a subset of ` B ` , then their predecessor classes are also
     subsets. $)
  predpredss $p |- ( A C_ B -> Pred ( R , A , X ) C_ Pred ( R , B , X ) ) $=
    ( wss ccnv csn cima cin cpred ssrin df-pred 3sstr4g ) ABEACFDGHZIBNIACDJBCD
    JABNKACDLBCDLM $.
    $( [2-Feb-2011] $)

  $( The predecessor class of ` A ` is a subset of ` A ` $)
  predss $p |- Pred ( R , A , X ) C_ A $=
    ( cpred wss ccnv csn cima cin inss1 df-pred sseq1i mpbir ) ABCDZAEABFCGHZIZ
    AEAOJNPAABCKLM $.
    $( [2-Feb-2011] $)

  $( Another subset/predecessor class relationship. $)
  sspred $p |- ( ( B C_ A /\ Pred ( R , A , X ) C_ B ) ->
                 Pred ( R , A , X ) = Pred ( R , B , X ) ) $=
    ( wss cin wceq ccnv csn cima cpred sseqin2 df-pred sseq1i df-ss in32
    eqeq1i 3bitri wa ineq1 eqeq1d biimpa eqeq12i biimpri eqcoms syl syl2anb )
    BAEABFZBGZUHCHDIJZFZAUJFZGZACDKZBCDKZGZUNBEZBALUQULBEULBFZULGUMUNULBACDMZNU
    LBOURUKULAUJBPQRUIUMSBUJFZULGZUPUIUMVAUIUKUTULUHBUJTUAUBUPULUTUPULUTGUNULUO
    UTUSBCDMUCUDUEUFUG $.
    $( [6-Feb-2011] $)

  ${
    $d R y $.  $d X y $.
    dfpred2.1 $e |- X e. _V $.
    $( An alternate definition of predecessor class when ` X ` is a set. $)
    dfpred2 $p |- Pred ( R , A , X ) = ( A i^i { y | y R X } ) $=
      ( cpred ccnv csn cima cin cv wbr cab df-pred cvv wcel wceq iniseg ax-mp
      ineq2i eqtri ) BCDFBCGDHIZJBAKDCLAMZJBCDNUBUCBDOPUBUCQEACDORSTUA $.
      $( [8-Feb-2011] $)
  $}

  ${
    elpredim.1 $e |- X e. _V $.
    $( Membership in a predecessor class - implicative version. $)
    elpredim $p |- ( Y e. Pred ( R , A , X ) -> Y R X ) $=
      ( cpred wcel ccnv csn cima wa wbr cin df-pred eleq2i elin bitri cop cvv
      wb elimasng opelcnvg bitrd mpan biimpd pm2.43i df-br sylibr adantl sylbi
      ) DABCFZGZDAGZDBHZCIJZGZKZDCBLZULDAUOMZGUQUKUSDABCNODAUOPQUPURUMUPDCRBGZU
      RUPUTUPUPUTCSGZUPUPUTTEVAUPKUPCDRUNGUTUNCDSUOUACDSUOBUBUCUDUEUFDCBUGUHUIU
      J $.
      $( [12-May-2012] $) $( [9-May-2012] $)
  $}

  ${
    elpred.1 $e |- Y e. _V $.
    $( Membership in a predecessor class. $)
    elpred $p |- ( X e. D -> ( Y e. Pred ( R , A , X ) <->
                                ( Y e. A /\ Y R X ) ) ) $=
      ( wcel cpred ccnv csn cima wa wbr wb cin df-pred eleq2i elin bitri a1i
      eliniseg anbi2d bitrd ) DBGZEACDHZGZEAGZECIDJKZGZLZUGEDCMZLUFUJNUDUFEAUHO
      ZGUJUEULEACDPQEAUHRSTUDUIUKUGCDEBFUAUBUC $.
      $( [4-Feb-2011] $)
  $}

  $( Membership in a predecessor class. $)
  elpredg $p |- ( ( X e. B /\ Y e. A ) ->
                  ( Y e. Pred ( R , A , X ) <-> Y R X ) ) $=
    ( wcel wa cpred ccnv csn cima wbr wb cin df-pred eleq2i elin bitri baib
    adantl cop elimasng df-br a1i brcnvg 3bitr2d bitrd ) DBFZEAFZGZEACDHZFZECIZ
    DJKZFZEDCLZUIULUOMUHULUIUOULEAUNNZFUIUOGUKUQEACDOPEAUNQRSTUJUODEUAUMFZDEUML
    ZUPUMDEBAUBUSURMUJDEUMUCUDDEBACUEUFUG $.
    $( [17-Apr-2011] $)

  ${
    $d A y $.  $d F y $.  $d G y $.  $d X y $.  $d R y $.
    predreseq.1 $e |- X e. _V $.
    $( Equality of restriction to predecessor classes. $)
    predreseq $p |- ( ( F Fn A /\ G Fn A ) ->
                       ( ( F |` Pred ( R , A , X ) ) =
                         ( G |` Pred ( R , A , X ) ) <->
                         A. y e. A ( y R X -> ( F ` y ) = ( G ` y ) ) ) ) $=
      ( wfn wa cpred cres wceq cv cfv wral wbr wi wss wb predss fvreseq mpan2
      wcel wal df-ral cvv visset elpred ax-mp imbi1i albii impexp 3bitri
      bitr4i syl6bb ) DBHEBHIZDBCFJZKEUQKLZAMZDNUSENLZAUQOZUSFCPZUTQZABOZUPUQBR
      URVASBCFTABUQDEUAUBVAUSBUCZVCQZAUDZVDVAUSUQUCZUTQZAUDVEVBIZUTQZAUDVGUTAUQ
      UEVIVKAVHVJUTFUFUCVHVJSGBUFCFUSAUGUHUIUJUKVKVFAVEVBUTULUKUMVCABUEUNUO $.
      $( [8-Feb-2011] $)
  $}

  ${
    predasetex.1 $e |- A e. _V $.
    $( The predecessor class exists when ` A ` does. $)
    predasetex $p |- Pred ( R , A , X ) e. _V $=
      ( cpred ccnv csn cima cin cvv df-pred inex1 eqeltri ) ABCEABFCGHZIJABCKAN
      DLM $.
      $( [8-Feb-2011] $)
  $}

  ${
    $d R x z $.  $d R y z $.  $d A x z $.  $d A y z $.
    $( Change the bound variable in the statement stating that ` R ` is
       set-like. $)
    cbvsetlike $p |- ( A. x e. A Pred ( R , A , x ) e. _V <->
                        A. y e. A Pred ( R , A , y ) e. _V ) $=
      ( vz cv cpred cvv wcel wral weq predeq3 eleq1d cbvralv bitr4i ) CDAFZGZHI
      ZACJCDEFZGZHIZECJCDBFZGZHIZBCJRUAAECAEKQTHCDPSLMNUDUABECBEKUCTHCDUBSLMNO
      $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d x y R $.  $d x A $.
    $( Alternate definition of founded relation. $)
    dffr4 $p |- ( R Fr A <->
                   A. x ( ( x C_ A /\ x =/= (/) )
                          -> E. y e. x Pred ( R , x , y ) = (/) ) ) $=
      ( wfr cv wss c0 wne wa ccnv csn cima cin wceq wrex wi wal cpred dffr3
      df-pred eqeq1i rexbii imbi2i albii bitr4i ) CDEAFZCGUGHIJZUGDKBFZLMNZHOZB
      UGPZQZARUHUGDUISZHOZBUGPZQZARABCDTUQUMAUPULUHUOUKBUGUNUJHUGDUIUAUBUCUDUEU
      F $.
      $( [2-Feb-2011] $)
  $}

  ${
    $( Membership in the predecessor class implies membership in the base
       class. $)
    predel $p |- ( Y e. Pred ( R , A , X ) -> Y e. A ) $=
      ( cpred wcel ccnv csn cima cin df-pred eleq2i elin simplbi sylbi ) DABCEZ
      FDABGCHIZJZFZDAFZPRDABCKLSTDQFDAQMNO $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d R z $.  $d A z $.  $d X z $.  $d Y z $.
    $( Property of the precessor class for partial orderings. $)
    predpo $p |- ( ( R Po A /\ X e. A ) ->
                   ( Y e. Pred ( R , A , X ) ->
                     Pred ( R , A , Y ) C_ Pred ( R , A , X ) ) ) $=
      ( vz wpo wcel wa cpred wss predel w3a cv wbr wi wb elpredg adantll potr
      3exp2 com24 imp31 com13 ex com14 sylbid com23 3imp imdistand visset
      elpred 3ad2ant3 adantl 3ad2ant1 3imtr4d ssrdv 3exp mpdi ) ABFZCAGZHZDABCI
      ZGZDAGZABDIZVBJZABCDKVAVCVDVFVAVCVDLZEVEVBVGEMZAGZVHDBNZHZVIVHCBNZHZVHVEG
      ZVHVBGZVGVIVJVLVAVCVDVIVJVLOOZVAVDVCVPVAVDVCVPOVAVDHZVCDCBNZVPUTVDVCVRPUS
      AABCDQRVJVRVIVQVLVJVRVIVQVLOOVQVIVJVRHZVLUSUTVDVIVSVLOZOUSVIVDUTVTUSVIVDU
      TVTAVHDCBSTUAUBUCUDUEUFUDUGUHUIVDVAVNVKPVCAABDVHEUJZUKULVAVCVOVMPZVDUTWBU
      SAABCVHWAUKUMUNUOUPUQUR $.
      $( [12-May-2012] $) $( [28-Apr-2012] $)
  $}

  $( Property of the predecessor class for strict orderings. $)
  predso $p |- ( ( R Or A /\ X e. A ) ->
                  ( Y e. Pred ( R , A , X ) ->
                    Pred ( R , A , Y ) C_ Pred ( R , A , X ) ) ) $=
    ( wor wpo wcel cpred wss wi sopo predpo sylan ) ABEABFCAGDABCHZGABDHNIJABKA
    BCDLM $.
    $( [11-Feb-2011] $)

  ${
    predbr.1 $e |- X e. _V $.
    predbr.2 $e |- Y e. _V $.
    $( If a set is in the predecessor class, then it is less than the base
       element. $)
    predbr $p |- ( Y e. Pred ( R , A , X ) -> Y R X ) $=
      ( cpred wcel wbr wa cvv wb elpred ax-mp biimpi simprd ) DABCGHZDAHZDCBIZQ
      RSJZCKHQTLEAKBCDFMNOP $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.  $d X x y $.  $d Y y $.
    $( Closed form of ~ predbr . $)
    predbrg $p |- ( ( X e. A /\ Y e. B ) ->
                     ( Y e. Pred ( R , A , X ) -> Y R X ) ) $=
      ( vy vx cv cpred wcel wbr wi wceq predeq3 eleq2d breq2 imbi12d eleq1
      breq1 visset predbr vtocl2g ) FHZACGHZIZJZUCUDCKZLUCACDIZJZUCDCKZLEUHJZED
      CKZLGFDEABUDDMZUFUIUGUJUMUEUHUCACUDDNOUDDUCCPQUCEMUIUKUJULUCEUHRUCEDCSQAC
      UDUCGTFTUAUB $.
      $( [13-Apr-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( If ` R ` is set-like in ` A ` then all predecessors classes of elements
       of ` A ` exist. $)
    setlikespec $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V )
                         -> Pred ( R , A , X ) e. _V ) $=
      ( cv cpred cvv wcel wceq predeq3 eleq1d rcla4va ) BCAEZFZGHBCDFZGHADBMDIN
      OGBCMDJKL $.
      $( [20-Feb-2011] $)
  $}

  $( Idempotent law for the predecessor class. $)
  predidm $p |- Pred ( R , Pred ( R , A , X ) , X ) = Pred ( R , A , X ) $=
    ( cpred ccnv csn cima cin df-pred ineq1i inidm ineq2i inass 3eqtr4ri
    3eqtr4i ) ABCDZBECFGZHAQHZQHZPBCDPPRQABCIZJPBCIAQQHZHRSPUAQAQKLAQQMTNO $.
    $( [29-Mar-2011] $)

  $( Intersection law for predecessor classes. $)
  predin $p |- Pred ( R , ( A i^i B ) , X ) =
               ( Pred ( R , A , X ) i^i Pred ( R , B , X ) ) $=
    ( cin ccnv csn cima cpred inindir df-pred ineq12i 3eqtr4i ) ABEZCFDGHZEAOEZ
    BOEZENCDIACDIZBCDIZEABOJNCDKRPSQACDKBCDKLM $.
    $( [29-Mar-2011] $)

  $( Union law for predecessor classes. $)
  predun $p |- Pred ( R , ( A u. B ) , X ) =
               ( Pred ( R , A , X ) u. Pred ( R , B , X ) ) $=
    ( cun ccnv csn cima cin cpred indir df-pred uneq12i 3eqtr4i ) ABEZCFDGHZIAP
    IZBPIZEOCDJACDJZBCDJZEABPKOCDLSQTRACDLBCDLMN $.
    $( [29-Mar-2011] $)

  $( Difference law for predecessor classes. $)
  preddif $p |- Pred ( R , ( A \ B ) , X ) =
                  ( Pred ( R , A , X ) \ Pred ( R , B , X ) ) $=
    ( cdif ccnv csn cima cin cpred indifdir df-pred difeq12i 3eqtr4i ) ABEZCFDG
    HZIAPIZBPIZEOCDJACDJZBCDJZEABPKOCDLSQTRACDLBCDLMN $.
    $( [14-Apr-2011] $)

  ${
    $d X y $.  $d B y $.
    $( The predecessor under the epsilon relationship is equivalent to an
       intersection.  (The proof was shortened by Andrew Salmon,
       27-Aug-2011.) $)
    predep $p |- ( X e. B -> Pred ( _E , A , X ) = ( A i^i X ) ) $=
      ( vy wcel cep cpred ccnv csn cima cin df-pred cv wbr cab wrel wceq
      relcnv relimasn ax-mp cvv wb visset brcnvg mpan2 epelg bitrd abbi1dv
      syl5eq ineq2d ) CBEZAFCGAFHZCIJZKACKAFCLUKUMCAUKUMCDMZULNZDOZCULPUMUPQFRD
      CULSTUKUODCUKUOUNCFNZUNCEUKUNUAEUOUQUBDUCCUNBUAFUDUEUNCBUFUGUHUIUJUI $.
      $( [27-Aug-2011] $) $( [27-Mar-2011] $)
  $}

  $( For an ordinal, the predecessor under ` _E ` and ` On ` is an identity
     relationship. $)
  predon $p |- ( A e. On -> Pred ( _E , On , A ) = A ) $=
    ( con0 wcel cep cpred cin predep wss wceq onss sseqin2 sylib eqtrd ) ABCZBD
    AEBAFZABBAGNABHOAIAJABKLM $.
    $( [27-Mar-2011] $)

  $( The epsilon relationship is set-like. $)
  epsetlike $p |- A. x e. A Pred ( _E , A , x ) e. _V $=
    ( cep cv cpred cvv wcel cin predep incom syl6eq inex1g eqeltrd rgen ) BCADZ
    EZFGABOBGZPOBHZFQPBOHRBBOIBOJKOBBLMN $.
    $( [27-Mar-2011] $)

  ${
    $d A x $.  $d B x $.
    $( If ` R ` is set-like over ` A ` , then it is set-like over any subclass
       of ` A ` . $)
    setlikess $p |- ( ( A C_ B /\ A. x e. B Pred ( R , B , x ) e. _V ) ->
                       A. x e. A Pred ( R , A , x ) e. _V ) $=
      ( wss cv cpred cvv wcel wral ssralv wi predpredss ssexg ex syl ralimdv
      syld imp ) BCEZCDAFZGZHIZACJZBDUAGZHIZABJZTUDUCABJUGUCABCKTUCUFABTUEUBEZU
      CUFLBCDUAMUHUCUFUEUBHNOPQRS $.
      $( [28-Mar-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.  $d X y $.
    $( A property of classes that are downward closed under predecessor. $)
    preddowncl $p |- ( ( B C_ A /\ A. x e. B Pred ( R , A , x ) C_ B ) ->
                        ( X e. B ->
                          Pred ( R , B , X ) = Pred ( R , A , X ) ) ) $=
      ( vy vz wss cv cpred wral wa wcel wceq wi eleq1 predeq3 eqeq12d imbi12d
      imbi2d predpredss ad2antrr wbr weq sseq1d rcla4cv imp sseld visset
      predbr a1i jcad wb elpred adantl mpbird ssrdv adantll eqssd ex vtoclg
      pm2.43b ) CBHZBDAIZJZCHZACKZLZECMZCDEJZBDEJZNZVHFIZCMZCDVMJZBDVMJZNZOZOVH
      VIVLOZOFECVMENZVRVSVHVTVNVIVQVLVMECPVTVOVJVPVKCDVMEQBDVMEQRSTVHVNVQVHVNLV
      OVPVCVOVPHVGVNCBDVMUAUBVGVNVPVOHVCVGVNLZGVPVOWAGIZVPMZWBVOMZOZWCWBCMZWBVM
      DUCZLZOZWAWCWFWGWAVPCWBVGVNVPCHZVFWJAVMCAFUDVEVPCBDVDVMQUEUFUGUHWCWGOWABD
      VMWBFUIGUIZUJUKULVNWEWIUMVGVNWDWHWCCCDVMWBWKUNTUOUPUQURUSUTVAVB $.
      $( [13-Apr-2011] $)
  $}

  $( Given a partial ordering, ` X ` is not a member of its predecessor
     class. $)
  predpoirr $p |- ( R Po A -> -. X e. Pred ( R , A , X ) ) $=
    ( wpo wcel cpred wn wa wbr poirr wb elpredg anidms notbid syl5ibr exp3a
    pm2.43b predel con3i pm2.61d1 ) ABDZCAEZCABCFEZGZUAUBUDUBUAUBUDUAUBHUDUBCCB
    IZGACBJUBUCUEUBUCUEKAABCCLMNOPQUCUBABCCRST $.
    $( [17-Apr-2011] $)

  $( Given a founded relationship, ` X ` is not a member of its predecessor
     class. $)
  predfrirr $p |- ( R Fr A -> -. X e. Pred ( R , A , X ) ) $=
    ( wfr wcel cpred wn wa wbr frirrc wb elpredg anidms notbid syl5ibr exp3a
    pm2.43b predel con3i pm2.61d1 ) ABDZCAEZCABCFEZGZUAUBUDUBUAUBUDUAUBHUDUBCCB
    IZGABCJUBUCUEUBUCUEKAABCCLMNOPQUCUBABCCRST $.
    $( [22-Apr-2011] $)


  $( The predecessor class over ` (/) ` is always ` (/) ` $)
  pred0 $p |- Pred ( R , (/) , X ) = (/) $=
    ( c0 cpred ccnv csn cima cin df-pred incom in0 3eqtri ) CABDCAEBFGZHMCHCCAB
    ICMJMKL $.
    $( [16-Apr-2011] $)

