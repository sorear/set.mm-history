$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of functions and mappings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A condition for subset trichotomy for functions. $)
  funpsstri $p |- ( ( Fun H /\ ( F C_ H /\ G C_ H ) /\
                      ( dom F C_ dom G \/ dom G C_ dom F ) ) ->
                    ( F C. G \/ F = G \/ G C. F ) ) $=
    ( wfun wss wa cdm wo w3a wpss wceq w3o cres wi funssres ex anim12d sseq12
    wb ancoms orbi12d ssres2 orim12i syl5bi syl6 3imp sspsstri sylib ) CDZACEZB
    CEZFZAGZBGZEZUNUMEZHZIABEZBAEZHZABJABKBAJLUIULUQUTUIULCUMMZAKZCUNMZBKZFZUQU
    TNUIUJVBUKVDUIUJVBCAOPUIUKVDCBOPQVEVAVCEZVCVAEZHUTUQVEVFURVGUSVAAVCBRVDVBVG
    USSVCBVAARTUAUOVFUPVGUMUNCUBUNUMCUBUCUDUEUFABUGUH $.
    $( [19-Apr-2011] $)

  ${
    $d F p x y z $.  $d G p x y z $.
    $( If a class ` F ` is a proper subset of a function ` G ` , then
       ` dom F C. dom G ` . $)
    fundmpss $p |- ( Fun G -> ( F C. G -> dom F C. dom G ) ) $=
      ( vx vy vz vp wfun wpss cdm wss wn wa wi pssss dmss syl a1i cv wbr wex
      cdif wcel wne c0 pssdifn0 df-pss n0 bicomi 3imtr4i adantl wrel funrel
      reldif cop wceq elrel eleq1 df-br syl6bbr biimpcd 2eximdv mpd ex adantr
      difss ssbri eximi brdif simprbi ssbrd ad2antlr weq wal dffun2 ax4 a4s
      breq2 biimprd syl6 exp3a simplbi syl5 imp adantlr com23 mpdd 19.23adv
      mtod jcad eximdv syld dfss2 notbii exanali visset eldm anbi12i exbii
      3bitr2i sylibr dfpss3 syl6ibr ) BGZABHZAIZBIZJZXFXEJZKZLXEXFHXCXDXGXIXDXG
      MXCXDABJZXGABNZABOPQXCXDXIXCXDLZCRZDRZBSZDTZXMERZASZETZKZLZCTZXIXLFRZBAUA
      ZUBZFTZYBXDYFXCXJABUCLYDUDUCZXDYFABUEABUFYGYFFYDUGUHUIUJXLYEYBFXLYEXMXNYD
      SZDTZCTZYBXCYEYJMZXDXCYDUKZYKXCBUKZYLBULBAUMPYLYEYJYLYELZYCXMXNUNZUOZDTCT
      YJCDYCYDUPYNYPYHCDYEYPYHMYLYPYEYHYPYEYOYDUBYHYCYOYDUQXMXNYDURUSUTUJVAVBVC
      PVDXLYIYACXLYIXPXTYIXPMXLYHXODYDBXMXNBAVEVFVGQXLYHXTDXLYHXTXLYHLZXSXMXNAS
      ZYHYRKZXLYHXOYSXMXNBAVHZVIUJYQXRYREYQXRXMXQBSZYRXDXRUUAMXCYHXDABXMXQXKVJV
      KYQUUAXRYRXCYHUUAXRYRMZMZXDXCYHUUCXCXOUUCYHXCXOUUAUUBXCXOUUALZDEVLZUUBXCU
      UDUUEMZEVMZDVMZCVMZUUFXCYMUUICDEBVNVIUUHUUFCUUGUUFDUUFEVOVPVPPUUEYRXRXNXQ
      XMAVQVRVSVTYHXOYSYTWAWBWCWDWEWFWGWHVCWGWIWJWKWGVBXIXMXFUBZXMXEUBZMCVMZKUU
      JUUKKZLZCTYBXHUULCXFXEWLWMUUJUUKCWNUUNYACUUJXPUUMXTDXMBCWOZWPUUKXSEXMAUUO
      WPWMWQWRWSWTVCWIXEXFXAXB $.
      $( [20-Apr-2011] $)
  $}

  $( A function value is a member of the range plus null. $)
  fvrn0 $p |- ( Fun A -> ( A ` X ) e. ( ran A u. { (/) } ) ) $=
    ( wfun cdm wcel cfv crn c0 csn cun wa fvelrn elun1 syl wn wceq ndmfv 0ex
    snid eleq1 mpbiri elun2 adantl pm2.61dan ) ACZBADEZBAFZAGZHIZJEZUEUFKUGUHEU
    JBALUGUHUIMNUFOZUJUEUKUGHPZUJBAQULUGUIEZUJULUMHUIEHRSUGHUITUAUGUIUHUBNNUCUD
    $.
    $( [8-Jun-2011] $)

  ${
    $d y A $.  $d y C $.  $d y B $.  $d y D $.  $d x y $.
    mpteq.1 $e |- A = C $.
    mpteq.2 $e |- B = D $.
    $( An equality inference for the maps to notation. $)
    mpteqi $p |- ( x e. A |-> B ) = ( x e. C |-> D ) $=
      ( vy cv wcel wceq wa copab cmpt eleq2i eqeq2i anbi12i opabbii df-mpt
      3eqtr4i ) AIZBJZHIZCKZLZAHMUADJZUCEKZLZAHMABCNADENUEUHAHUBUFUDUGBDUAFOCEU
      CGPQRAHBCSAHDEST $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d F x $.  $d G x $.
    $( Equality of functions is determined by their values. $)
    eqfunfv $p |- ( ( Fun F /\ Fun G ) -> ( F = G <->
                    ( dom F = dom G /\
                      A. x e. dom F ( F ` x ) = ( G ` x ) ) ) ) $=
      ( cdm wfn wceq cv cfv wral wa wb wfun eqfnfv funfn syl2anb ) BBDZECCDZEBC
      FPQFAGZBHRCHFAPIJKBLCLAPQBCMBNCNO $.
      $( [10-Jul-2011] $) $( [19-Jun-2011] $)
  $}

  $( An identity for the mapping relationship under restriction. $)
  fresin $p |- ( F : A --> B -> ( F |` X ) : ( A i^i X ) --> B ) $=
    ( wfn crn wss wa cres cin wf wfun cdm wceq funres ineq1 dmres incom eqtri
    syl5eq anim12i df-fn 3imtr4i wi resss rnss ax-mp sstr2 df-f ) CAEZCFZBGZHCD
    IZADJZEZUMFZBGZHABCKUNBUMKUJUOULUQCLZCMZANZHUMLZUMMZUNNZHUJUOURVAUTVCDCOUTU
    SDJZUNVBUSADPVBDUSJVDCDQDUSRSTUACAUBUMUNUBUCUPUKGZULUQUDUMCGVECDUEUMCUFUGUP
    UKBUHUGUAABCUIUNBUMUIUC $.
    $( [10-Sep-2011] $) $( [4-Sep-2011] $)

  $( The value of a function at a restriction is either null or the same as the
     function itself. $)
  fvresval $p |- ( ( ( F |` B ) ` A ) = ( F ` A ) \/
                   ( ( F |` B ) ` A ) = (/) ) $=
    ( wcel wn wo cres cfv wceq c0 exmid fvres nfvres orim12i ax-mp ) ABDZPEZFAC
    BGHZACHIZRJIZFPKPSQTABCLABCMNO $.
    $( [10-Sep-2011] $) $( [4-Sep-2011] $)

  ${
    $d A y $.  $d x y z $.
    $( The range of a restricted operation class abstraction. $)
    rnoprab2 $p |- ran { <. <. x , y >. , z >. |
                          ( ( x e. A /\ y e. B ) /\ ph ) } =
                    { z | E. x e. A E. y e. B ph } $=
      ( cv wcel wa copab2 crn wex cab wrex rnoprab r2ex abbii eqtr4i ) BGEHCGFH
      IAIZBCDJKSCLBLZDMACFNBENZDMSBCDOUATDABCEFPQR $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  ${
    $d A w z $.  $d B w z $.  $d C w z $.  $d x y w z $.
    mpt2fun.1 $e |- F = ( x e. A , y e. B |-> C ) $.
    $( The maps-to notation for an operation is always a function. $)
    mpt2fun $p |- Fun F $=
      ( vz vw wfun cv wcel wa wceq copab2 wmo weq wi wal eqtr3 ad2ant2l gen2
      eqeq1 anbi2d mo4 mpbir funoprab cmpt2 df-mpt2 eqtri funeqi ) FJAKCLBKDLMZ
      HKZENZMZABHOZJUOABHUOHPUOULIKZENZMZMHIQZRZISHSVAHIUNURUTULULUMUQETUAUBUOU
      SHIUTUNURULUMUQEUCUDUEUFUGFUPFABCDEUHUPGABHCDEUIUJUKUF $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    $( The maps-to notation always describes a relationship. $)
    mptrel $p |- Rel ( x e. A |-> B ) $=
      ( vy cmpt wrel cv wcel wceq wa copab relopab df-mpt releqi mpbir ) ABCEZF
      AGBHDGCIJZADKZFQADLPRADBCMNO $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d F x y z $.  $d G x y z $.
    $( Given two functions with equal domains, equality only requires one
       direction of the subset relationship. $)
    funsseq $p |- ( ( Fun F /\ Fun G /\ dom F = dom G ) ->
                     ( F = G <-> F C_ G ) ) $=
      ( vz vx vy wfun cdm wceq w3a wss wa simpl cv wcel wrel wi simpl2 funrel
      syl cop wex wbr eleq1 df-br syl6bbr imbi12d cfv visset funbrfv simpl3
      eleq2d simpll2 simplr simpr funssfv syl111anc eqeq1d biimp3a wb simp1l1
      simp2 funbrfvb syl11anc mpbid 3exp sylbird breldm syl5 mpdd syl5cbir
      19.23advv elrel mpand pm2.43d ssrdv ex ancld impbid2 eqss syl5bb ) AFZBFZ
      AGZBGZHZIZABJZBAJZKZWGABHWFWIWGWGWHLWFWGWHWFWGWHWFWGKZCBAWJCMZBNZWKANZWJB
      OZWLWLWMPZWJWBWNWAWBWEWGQZBRSWJWKDMZEMZTZHZEUADUAWOWNWLKWJWTWODEWTWOWQWRB
      UBZWQWRAUBZPWJWTWLXAWMXBWTWLWSBNXAWKWSBUCWQWRBUDUEWTWMWSANXBWKWSAUCWQWRAU
      DUEUFWJXAWQBUGZWRHZXBWJWBXAXDPWPWQWRBEUHZUISWJWQWDNZXDXBPZXAWJXFWQWCNZXGW
      JWCWDWQWAWBWEWGUJUKWJXHXDXBWJXHXDIZWQAUGZWRHZXBWJXHXDXKWJXHKZXCXJWRXLWBWG
      XHXCXJHWAWBWEWGXHULWFWGXHUMWJXHUNWQBAUOUPUQURXIWAXHXKXBUSWAWBWEWGXHXDUTWJ
      XHXDVAWQWRAXEVBVCVDVEVFWQWRBDUHVGVHVIVJVKDEWKBVLVHVMVNVOVPVQVRABVSVT $.
      $( [12-May-2012] $) $( [24-Apr-2012] $)
  $}

