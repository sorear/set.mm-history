$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of functions and mappings
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A condition for subset trichotomy for functions $)
  funpsstri $p |- ( ( Fun H /\ ( F C_ H /\ G C_ H ) /\
                      ( dom F C_ dom G \/ dom G C_ dom F ) ) ->
                    ( F C. G \/ F = G \/ G C. F ) ) $=
    ( wfun wss wa cdm wo w3a wpss wceq w3o cres wi funssres ex anim12d sseq12
    wb ancoms orbi12d ssres2 orim12i syl5bi syl6 3imp sspsstri sylib ) CDZACEZB
    CEZFZAGZBGZEZUNUMEZHZIABEZBAEZHZABJABKBAJLUIULUQUTUIULCUMMZAKZCUNMZBKZFZUQU
    TNUIUJVBUKVDUIUJVBCAOPUIUKVDCBOPQVEVAVCEZVCVAEZHUTUQVEVFURVGUSVAAVCBRVDVBVG
    USSVCBVAARTUAUOVFUPVGUMUNCUBUNUMCUBUCUDUEUFABUGUH $.
    $( [22-Apr-2011] $) $( [19-Apr-2011] $)

  ${
    $d F p x y z $.  $d G p x y z $.
    $( If a class ` F ` is a proper subset of a function ` G ` , then
       ` dom F C. dom G ` . $)
    fundmpss $p |- ( Fun G -> ( F C. G -> dom F C. dom G ) ) $=
      ( vx vy vz vp wfun wpss cdm wss wn wa wi pssss dmss syl a1i cv wbr wex
      cdif wcel wne c0 pssdifn0 df-pss n0 bicomi 3imtr4i adantl wrel funrel
      reldif cop wceq elrel eleq1 df-br syl6bbr biimpcd 19.22dvv mpd ex adantr
      difss ssbri 19.22i brdif pm3.27bi ssbrd ad2antlr weq wal dffun2 ax-4 a4s
      breq2 biimprd syl6 exp3a pm3.26bi syl5 imp adantlr com23 mpdd 19.23adv
      mtod jcad 19.22dv syld dfss2 notbii exanali visset eldm anbi12i exbii
      3bitr2i sylibr dfpss3 syl6ibr ) BGZABHZAIZBIZJZXFXEJZKZLXEXFHXCXDXGXIXDXG
      MXCXDABJZXGABNZABOPQXCXDXIXCXDLZCRZDRZBSZDTZXMERZASZETZKZLZCTZXIXLFRZBAUA
      ZUBZFTZYBXDYFXCXJABUCLYDUDUCZXDYFABUEABUFYGYFFYDUGUHUIUJXLYEYBFXLYEXMXNYD
      SZDTZCTZYBXCYEYJMZXDXCYDUKZYKXCBUKZYLBULBAUMPYLYEYJYLYELZYCXMXNUNZUOZDTCT
      YJCDYCYDUPYNYPYHCDYEYPYHMYLYPYEYHYPYEYOYDUBYHYCYOYDUQXMXNYDURUSUTUJVAVBVC
      PVDXLYIYACXLYIXPXTYIXPMXLYHXODYDBXMXNBAVEVFVGQXLYHXTDXLYHXTXLYHLZXSXMXNAS
      ZYHYRKZXLYHXOYSXMXNBAVHZVIUJYQXRYREYQXRXMXQBSZYRXDXRUUAMXCYHXDABXMXQXKVJV
      KYQUUAXRYRXCYHUUAXRYRMZMZXDXCYHUUCXCXOUUCYHXCXOUUAUUBXCXOUUALZDEVLZUUBXCU
      UDUUEMZEVMZDVMZCVMZUUFXCYMUUICDEBVNVIUUHUUFCUUGUUFDUUFEVOVPVPPUUEYRXRXNXQ
      XMAVQVRVSVTYHXOYSYTWAWBWCWDWEWFWGWHVCWGWIWJWKWGVBXIXMXFUBZXMXEUBZMCVMZKUU
      JUUKKZLZCTYBXHUULCXFXEWLWMUUJUUKCWNUUNYACUUJXPUUMXTDXMBCWOZWPUUKXSEXMAUUO
      WPWMWQWRWSWTVCWIXEXFXAXB $.
      $( [22-Apr-2011] $) $( [20-Apr-2011] $)
  $}


  $( A function value is a member of the range plus null $)
  fvrn0 $p |- ( Fun A -> ( A ` X ) e. ( ran A u. { (/) } ) ) $=
    ( wfun cdm wcel cfv crn c0 csn cun wa fvelrn elun1 syl wn wceq ndmfv 0ex
    snid eleq1 mpbiri elun2 adantl pm2.61dan ) ACZBADEZBAFZAGZHIZJEZUEUFKUGUHEU
    JBALUGUHUIMNUFOZUJUEUKUGHPZUJBAQULUGUIEZUJULUMHUIEHRSUGHUITUAUGUIUHUBNNUCUD
    $.
    $( [8-Jun-2011] $)


  ${
    $d y A $.  $d y C $.  $d y B $.  $d y D $.  $d x y $.
    mpteq.1 $e |- A = C $.
    mpteq.2 $e |- B = D $.
    $( An equality inference for the maps to notation. $)
    mpteqi $p |- ( x e. A |-> B ) = ( x e. C |-> D ) $=
      ( vy cv wcel wceq wa copab cmpt eleq2i eqeq2i anbi12i opabbii df-mpt
      3eqtr4i ) AIZBJZHIZCKZLZAHMUADJZUCEKZLZAHMABCNADENUEUHAHUBUFUDUGBDUAFOCEU
      CGPQRAHBCSAHDEST $.
      $( [31-Oct-2010] $) $( [27-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d y B $.
    mptfn.1 $e |- F = ( x e. A |-> B ) $.
    $( The mapping notation defines a function over ` A ` $)
    mptfn $p |- ( A. x e. A B e. _V <-> F Fn A ) $=
      ( vy cmpt cv wcel wceq wa copab df-mpt eqtri fnopab2g ) AFBCDDABCGAHBIFHC
      JKAFLEAFBCMNO $.
      $( [27-Mar-2011] $) $( [21-Mar-2011] $)
  $}

  ${
    $d A y $.  $d B y $.  $d x y $.
    rnmpt.1 $e |- F = ( x e. A |-> B ) $.
    $( The range of the mapping operation $)
    rnmpt $p |- ran F = { y | E. x e. A y = B } $=
      ( crn cv wcel wceq wa copab wrex cab cmpt df-mpt eqtri rneqi rnopab2 ) EG
      AHCIBHDJZKABLZGTACMBNEUAEACDOUAFABCDPQRABCDSQ $.
      $( [27-Mar-2011] $) $( [21-Mar-2011] $)
  $}

