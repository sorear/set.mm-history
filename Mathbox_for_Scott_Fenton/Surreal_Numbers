$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Set up the new syntax for surreal numbers $)
  $c No <s bday $.
  $( Declare the class of all surreal numbers (see ~ df-no ). $)
  csur $a class No $.
  $( Declare the less than relationship over surreal numbers (see
     ~ df-slt ). $)
  cslt $a class <s $.
  $( Declare the birthday function for surreal numbers (see ~ df-bday ). $)
  cbday $a class bday $.

  ${
    $d f a $.
    $( Define the class of surreal numbers.  The surreal numbers are a proper
       class of numbers developed by John H. Conway and introduced by Donald
       Knuth in 1975.  They form a proper class into which all ordered fields
       can be embedded.  The approach we take to defining them was first
       introduced by Hary Goshnor, and is based on the conception of a "sign
       expansion" of a surreal number.  We define the surreals as
       ordinal-indexed sequences of ` 1o ` and ` 2o ` , analagous to Goshnor's
       ` ( - ) ` and ` ( + ) ` .

       After introducing this definition, we will abstract away from it using
       axioms that Norman Alling developed in "Foundations of Analysis over
       Surreal Number Fields."  This is done in a effort to be agnostic towards
       the exact implementation of surreals. $)
    df-no $a |- No = { f | E. a e. On f : a --> { 1o , 2o } } $.
  $}

  ${
    $d f g x y $.
    $( Next, we introduce surreal less-than, a comparison relationship over the
       surreals by lexicographically ordering them. $)
    df-slt $a |- <s = { <. f , g >. | ( ( f e. No /\ g e. No ) /\
                         E. x e. On (
                            A. y e. x ( f ` y ) = ( g ` y ) /\
                              ( f ` x ) { <. 1o , (/) >. ,
                                          <. 1o , 2o >. ,
                                          <. (/) , 2o >. } ( g ` x ) ) ) } $.
  $}

  $( Finally, we introduce the birthday function.  This function maps each
     surreal to an ordinal.  In our implementation, this is the domain of the
     sign function.  The important properties of this function are established
     later. $)
  df-bday $a |- bday = ( x e. No |-> dom x ) $.


  ${
    $d A f x $.
    $( Membership in the surreals.  (Shortened proof on 2012-Apr-14, SF).
       (Contributed by Scott Fenton, 11-Jun-2011.) $)
    elno $p |- ( A e. No <-> E. x e. On A : x --> { 1o , 2o } ) $=
      ( vf csur wcel cvv cv c1o c2o cpr con0 wrex elisset fex ancoms r19.23aiva
      wf wceq feq1 rexbidv df-no elab2g pm5.21nii ) BDEBFEZAGZHIJZBQZAKLZBDMUGU
      DAKUGUEKEUDUEUFKBNOPUEUFCGZQZAKLUHCBDFUIBRUJUGAKUEUFUIBSTCAUAUBUC $.
      $( [12-May-2012] $) $( [11-Jun-2011] $)
  $}

  ${
    $d A f g x y $.  $d B f g x y $.
    $( The value of the surreal less than relationship.  (Contributed by Scott
       Fenton, 14-Jun-2011.) $)
    sltval $p |- ( ( A e. No /\ B e. No ) ->
            ( A <s B <-> E. x e. On ( A. y e. x ( A ` y ) = ( B ` y ) /\
                         ( A ` x ) { <. 1o , (/) >. , <. 1o , 2o >. ,
                                     <. (/) , 2o >. } ( B ` x ) ) ) ) $=
      ( vf vg csur wcel wa cslt wbr cfv wceq wral cop con0 wrex fveq1 anbi12d
      cv c1o c0 c2o ctp eleq1 anbi1d eqeq1d ralbidv breq1d anbi2d eqeq2d breq2d
      rexbidv df-slt brabg bianabs ) CGHZDGHZIZCDJKBTZCLZUTDLZMZBATZNZVDCLZVDDL
      ZUAUBOUAUCOUBUCOUDZKZIZAPQZETZGHZFTZGHZIZUTVLLZUTVNLZMZBVDNZVDVLLZVDVNLZV
      HKZIZAPQZIUQVOIZVAVRMZBVDNZVFWBVHKZIZAPQZIUSVKIEFCDGGJVLCMZVPWFWEWKWLVMUQ
      VOVLCGUEUFWLWDWJAPWLVTWHWCWIWLVSWGBVDWLVQVAVRUTVLCRUGUHWLWAVFWBVHVDVLCRUI
      SUMSVNDMZWFUSWKVKWMVOURUQVNDGUEUJWMWJVJAPWMWHVEWIVIWMWGVCBVDWMVRVBVAUTVND
      RUKUHWMWBVGVFVHVDVNDRULSUMSABEFUNUOUP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( The value of the birthday function within the surreals.  (Contributed by
       Scott Fenton, 14-Jun-2011.) $)
    bdayval $p |- ( A e. No -> ( bday ` A ) = dom A ) $=
      ( vx csur wcel cdm cvv cbday cfv wceq dmexg cv dmeq df-bday fvmptg mpdan
      ) ACDAEZFDAGHPIACJBABKZEPCFGQALBMNO $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( A surreal is a function.  (Contributed by Scott Fenton, 16-Jun-2011.) $)
    nofun $p |- ( A e. No -> Fun A ) $=
      ( vx csur wcel cv c1o c2o cpr con0 wrex wfun elno ffun r19.23aivw sylbi
      wf ) ACDBEZFGHZAPZBIJAKZBALSTBIQRAMNO $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The domain of a surreal is an ordinal.  (Contributed by Scott Fenton,
       16-Jun-2011.) $)
    nodmon $p |- ( A e. No -> dom A e. On ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex elno eleq1d biimprcd r19.23aiv
      cdm fdm sylbi ) ACDBEZFGHZAIZBJKAPZJDZBALUAUCBJUAUCSJDUAUBSJSTAQMNOR $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The range of a surreal is a subset of the surreal signs.  (Contributed
       by Scott Fenton, 16-Jun-2011.) $)
    norn $p |- ( A e. No -> ran A C_ { 1o , 2o } ) $=
      ( vx csur wcel cv c1o c2o cpr con0 wrex crn wss elno frn r19.23aivw sylbi
      wf ) ACDBEZFGHZAQZBIJAKSLZBAMTUABIRSANOP $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  $( The domain of a surreal has the ordinal property.  (Contributed by Scott
     Fenton, 16-Jun-2011.) $)
  nodmord $p |- ( A e. No -> Ord dom A ) $=
    ( csur wcel cdm con0 word nodmon eloni syl ) ABCADZECJFAGJHI $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)


  ${
    $d x A $.
    $( An alternative condition for membership in ` No ` .  (Contributed by
       Scott Fenton, 21-Mar-2012.) $)
    elno2 $p |- ( A e. No <->
              ( Fun A /\ dom A e. On /\ ran A C_ { 1o , 2o } ) ) $=
      ( vx csur wcel wfun cdm con0 crn c1o c2o cpr wss w3a nofun nodmon norn wf
      3jca wa sylibr cv wrex simp2 wceq simpl eqidd df-fn sylanbrc anim1i 3impa
      wfn df-f feq2 rcla4ev syl11anc elno impbii ) ACDZAEZAFZGDZAHIJKZLZMZURUSV
      AVCANAOAPRVDBUAZVBAQZBGUBZURVDVAUTVBAQZVGUSVAVCUCVDAUTUKZVCSZVHUSVAVCVJUS
      VASZVIVCVKUSUTUTUDVIUSVAUEVKUTUFAUTUGUHUIUJUTVBAULTVFVHBUTGVEUTVBAUMUNUOB
      AUPTUQ $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  $( Another condition for membership in ` No ` .  (Contributed by Scott
     Fenton, 14-Apr-2012.) $)
  elno3 $p |- ( A e. No <-> ( A : dom A --> { 1o , 2o } /\ dom A e. On ) ) $=
    ( wfun cdm con0 wcel crn c1o c2o cpr wss w3a wa csur wf 3ancomb bitri elno2
    df-3an wfn anbi1i df-f funfn bitr4i 3bitr4i ) ABZACZDEZAFGHIZJZKZUEUILZUGLZ
    AMEUFUHANZUGLUJUEUIUGKULUEUGUIOUEUIUGRPAQUMUKUGUMAUFSZUILUKUFUHAUAUEUNUIAUB
    TUCTUD $.
    $( [12-May-2012] $) $( [14-Apr-2012] $)

  ${
    $d A a x y $.  $d B a x y $.
    $( Alternate expression for surreal less than.  Two surreals obey surreal
       less than iff they obey the sign ordering at the first place they
       differ.  (Contributed by Scott Fenton, 17-Jun-2011.) $)
    sltval2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B <->
                       ( A ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                       { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                       ( B ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                     ) ) $=
      ( vy vx wcel wa cfv wceq c1o c0 c2o con0 wne wn df-ne mtbiri adantl fveq2
      fvex csur cslt wbr cv wral cop ctp wrex crab cint sltval cvv w3o elisseti
      0ex 2on brtp 1n0 mpbi eqeq1 fvprc nsyl2 adantr 3jaoi sylbi onintrab sylib
      2on0 onelon expcom syl5 weq eqeq12d necon3bid onnminsb com12 syld con2bii
      syl6ibr r19.21aiv jca ex impac anass raleq breq12d anbi12d rcla4ev syl wi
      eqeq12 csuc 1on 0elon suc11 mp2an mpbir df-2o df-1o eqeq12i nemtbir eqcom
      wb syl6bb mtbi 3imtr4i elrab biimpri adantlr wss ssrab2 ne0i onint hbrab1
      sylancr hbint ax-17 hbfv hbne elrabf simprbi rcla4cv ad2antlr mtod simpll
      oninton ontri1 syl11anc mpbird intss1 syldan sylan2 fveq2d biimpd pm2.43d
      eqssd expimpd r19.23aiv impbid1 bitr4d ) AUAFBUAFGZABUBUCDUDZAHZUUBBHZIZD
      EUDZUEZUUFAHZUUFBHZJKUFJLUFKLUFUGZUCZGZEMUHZCUDZAHZUUNBHZNZCMUIZUJZAHZUUS
      BHZUUJUCZEDABUKUUAUVBUUMUUAUVBUUMUUAUVBGZUUSMFZUUEDUUSUEZUVBGZGZUUMUVCUVD
      UVEGZUVBGUVGUUAUVBUVHUUAUVBUVHUVCUVDUVEUVBUVDUUAUVBUUSULFZUVDUVBUUTJIZUVA
      KIZGZUVJUVALIZGZUUTKIZUVMGZUMUVIJKJLKLUUTUVAUUSATUUSBTUOLMUPUNZUVQUQUVLUV
      IUVNUVPUVJUVIUVKUVJUVOUVIUVJUVOJKIZJKNZUVROURJKPUSZUUTJKUTQUUSAVAVBZVCUVJ
      UVIUVMUWAVCUVMUVIUVOUVMUVKUVIUVMUVKLKIZLKNUWBOVHLKPUSZUVALKUTQUUSBVAVBRVD
      VEUUQCVFVGRZUVCUUEDUUSUVCUUBUUSFZUUCUUDNZOZUUEUWEUVCUWGUWEUVCUUBMFZUWGUVC
      UVDUWEUWHUWDUVDUWEUWHUUSUUBVIVJVKUWHUWEUWGUUQUWFCUUBCDVLZUUOUUPUUCUUDUWIU
      UOUUCUUPUUDUUNUUBASUUNUUBBSVMVNVOVPVQVPUWFUUEUUCUUDPVRVSVTWAWBWCUVDUVEUVB
      WDVGUULUVFEUUSMUUFUUSIZUUGUVEUUKUVBUUEDUUFUUSWEUWJUUHUUTUUIUVAUUJUUFUUSAS
      UUFUUSBSWFWGWHWIWBUULUVBEMUUFMFZUUGUUKUVBUWKUUGGZUUKUVBUWLUUKUUKUVBWJUWLU
      UKGZUUKUVBUWMUUHUUTUUIUVAUUJUWMUUFUUSAUUKUWLUUHUUINZUWJUUHJIZUUIKIGZUWOUU
      ILIZGZUUHKIUWQGZUMUUHUUIIZOZUUKUWNUWPUXAUWRUWSUWPUWTUVRUVTUUHJUUIKWKQUWRU
      WTLJIZUXBJWLZKWLZUXCUXDNZUVSURJMFZKMFZUXEUVSXCWMWNUXFUXGGUXCUXDJKJKWOVNWP
      WQLUXCJUXDWRWSWTXAUWRUWTJLIUXBUUHJUUILWKJLXBXDQUWSUWTKLIZUWBUXHUWCLKXBXEU
      UHKUUILWKQVDJKJLKLUUHUUIUUFATUUFBTUOUVQUVQUQUUHUUIPXFUWLUWNUUFUURFZUWJUWK
      UWNUXIUUGUXIUWKUWNGUUQUWNCUUFMCEVLZUUOUUPUUHUUIUXJUUOUUHUUPUUIUUNUUFASUUN
      UUFBSVMVNXGXHXIUWLUXIGZUUFUUSUXKUUFUUSXJZUUSUUFFZOZUXKUXMUUTUVAIZUXKUUTUV
      ANZUXOOUXKUUSUURFZUXPUXKUURMXJZUURKNZUXQUUQCMXKZUXIUXSUWLUURUUFXLZRUURXMX
      OUXQUVDUXPUUQUXPCEUUSMCEUURUUQCEMXNXPZUWKCXQCEUUTUVACEUUSAUUFAFCXQUYBXRCE
      UUSBUUFBFCXQUYBXRXSUUNUUSIZUUOUUPUUTUVAUYCUUOUUTUUPUVAUUNUUSASUUNUUSBSVMV
      NXTYAWIUUTUVAPVGUUGUXMUXOWJUWKUXIUUEUXODUUSUUFUUBUUSIUUCUUTUUDUVAUUBUUSAS
      UUBUUSBSVMYBYCYDUXKUWKUVDUXLUXNXCUWKUUGUXIYEUXIUVDUWLUXIUXRUXSUVDUXTUYAUU
      RYFXORUUFUUSYGYHYIUXIUUSUUFXJUWLUUFUURYJRYPYKYLZYMUWMUUFUUSBUYDYMWFYNWBYO
      YQYRYSYT $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $( The function value of a surreal is either a sign or the empty set.
       (Contributed by Scott Fenton, 22-Jun-2011.) $)
    nofv $p |- ( A e. No -> ( ( A ` X ) = (/) \/ ( A ` X ) = 1o \/
                               ( A ` X ) = 2o ) ) $=
      ( csur wcel cfv c0 wceq c1o c2o wo w3o cdm wn pm2.1 wi ndmfv a1i elisseti
      wa con0 wfun crn cpr wss nofun norn fvelrn ssel syl5com com23 imp 1on 2on
      ex elpr2 syl6ib syl11anc orim12d mpi 3orass sylibr ) ACDZBAEZFGZVCHGZVCIG
      ZJZJZVDVEVFKVBBALDZMZVIJVHVINVBVJVDVIVGVJVDOVBBAPQVBAUAZAUBZHIUCZUDZVIVGO
      AUEAUFVKVNSVIVCVMDZVGVKVNVIVOOVKVIVNVOVKVIVNVOOVKVISVCVLDVNVOBAUGVLVMVCUH
      UIUNUJUKVCHIHTULRITUMRUOUPUQURUSVDVEVFUTVA $.
      $( [10-Jul-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    $( ` (/) ` is not a surreal sign.  (Contributed by Scott Fenton,
       16-Jun-2011.) $)
    nosgnn0 $p |- -. (/) e. { 1o , 2o } $=
      ( c0 c1o c2o cpr wcel wceq wo wne 1n0 necom df-ne bitri mpbi csuc nsuceq0
      wn df-2o neeq2i bitr4i pm3.2ni 0ex elpr mtbir ) ABCDEABFZACFZGUDUEBAHZUDP
      ZIUFABHUGBAJABKLMACHZUEPBNZAHZUHBOUJAUIHUHUIAJCUIAQRSMACKMTABCUAUBUC $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    nosgnn0i.1 $e |- X e. { 1o , 2o } $.
    $( If ` X ` is a surreal sign, then it is not null.  (Contributed by Scott
       Fenton, 3-Aug-2011.) $)
    nosgnn0i $p |- (/) =/= X $=
      ( c0 wne wceq wn c1o c2o cpr wcel nosgnn0 eleq1 mpbiri mto df-ne mpbir )
      CADCAEZFQCGHIZJZKQSARJBCARLMNCAOP $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The restriction of a surreal to an ordinal is still a surreal.
       (Contributed by Scott Fenton, 4-Sep-2011.) $)
    noreson $p |- ( ( A e. No /\ B e. On ) -> ( A |` B ) e. No ) $=
      ( vx vy cv c1o c2o cpr wf con0 wrex wcel wa cres csur an32 cin onin elno
      wi adantr fresin adantl feq2 rcla4ev syl11anc sylbi r19.23aiva imp anbi1i
      ex 3imtr4i ) CEZFGHZAIZCJKZBJLZMDEZUNABNZIZDJKZAOLZUQMUSOLUPUQVAUOUQVATCJ
      UMJLZUOMZUQVAVDUQMVCUQMZUOMZVAVCUOUQPVFUMBQZJLZVGUNUSIZVAVEVHUOUMBRUAUOVI
      VEUMUNABUBUCUTVIDVGJURVGUNUSUDUEUFUGUKUHUIVBUPUQCASUJDUSSUL $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A k $.  $d B k $.

    $( If ` A <s B ` , then the sign of ` A ` at the first place they differ is
       either undefined or ` 1o ` (Contributed by Scott Fenton, 4-Sep-2011.) $)
    sltsgn1 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 1o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab c1o cop c2o wceq fvex adantr
      c0 cint ctp sltval2 w3o 0ex 2on elisseti brtp olc orc 3jaoi sylbi syl6bi
      wo ) ADEBDEFABGHCIZAJUOBJKCLMUAZAJZUPBJZNTONPOTPOUBHZUQTQZUQNQZUNZABCUCUS
      VAURTQZFZVAURPQZFZUTVEFZUDVBNTNPTPUQURUPARUPBRUEPLUFUGZVHUHVDVBVFVGVAVBVC
      VAUTUIZSVAVBVEVISUTVBVEUTVAUJSUKULUM $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)

    $( If ` A <s B ` , then the sign of ` B ` at the first place they differ is
       either undefined or ` 2o ` (Contributed by Scott Fenton, 4-Sep-2011.) $)
    sltsgn2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 2o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab c1o cop c2o wceq fvex adantl
      c0 cint ctp sltval2 w3o 0ex 2on elisseti brtp orc olc 3jaoi sylbi syl6bi
      wo ) ADEBDEFABGHCIZAJUOBJKCLMUAZAJZUPBJZNTONPOTPOUBHZURTQZURPQZUNZABCUCUS
      UQNQZUTFZVCVAFZUQTQZVAFZUDVBNTNPTPUQURUPARUPBRUEPLUFUGZVHUHVDVBVEVGUTVBVC
      UTVAUISVAVBVCVAUTUJZSVAVBVFVISUKULUM $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( If ` A <s B ` , then the intersection of all the ordinals that have
       differing signs in ` A ` and ` B ` exists.  (Contributed by Scott
       Fenton, 22-Feb-2012.) $)
    sltintdifex $p |- ( ( A e. No /\ B e. No ) ->
       ( A <s B -> |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. _V ) ) $=
      ( csur wcel wa wbr cfv wne con0 c1o c0 cop c2o wceq fvex fvprc df-ne mpbi
      wn cslt cv crab cint ctp cvv sltval2 w3o 0ex 2on elisseti 1n0 eqeq1 eqcom
      brtp syl6bb mtbiri syl con4i adantr 2on0 adantl 3jaoi sylbi syl6bi ) ADEB
      DEFABUAGCUBZAHVFBHICJUCUDZAHZVGBHZKLMKNMLNMUEGZVGUFEZABCUGVJVHKOZVILOZFZV
      LVINOZFZVHLOZVOFZUHVKKLKNLNVHVIVGAPVGBPUINJUJUKZVSUOVNVKVPVRVLVKVMVKVLVKT
      ZVQVLTVGAQVQVLKLOZKLIWATULKLRSVQVLLKOWAVHLKUMLKUNUPUQURUSZUTVLVKVOWBUTVOV
      KVQVKVOVTVMVOTVGBQVMVONLOZNLIWCTVANLRSVMVOLNOWCVILNUMLNUNUPUQURUSVBVCVDVE
      $.
      $( [22-Mar-2012] $) $( [22-Feb-2012] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.  $d X a x y $.
    $( Lemma for axfe (future) .  If the restrictions of two surreals to a
       given ordinal obey surreal less than, then so do the two surreals
       themselves. $)
    sltres $p |- ( ( A e. No /\ B e. No /\ X e. On ) ->
                      ( ( A |` X ) <s ( B |` X ) -> A <s B ) ) $=
      ( vy va wcel con0 wbr wa cfv wceq c1o c0 c2o wi imp wn syl sylc ex vx w3a
      csur cres cslt cv wral cop ctp wrex wne crab cint noreson 3adant2 3adant1
      cvv sltintdifex onintrab syl6ib syl11anc wss simpl3 cdm wo wb sltval2 w3o
      fvex 0ex 2on elisseti brtp 1n0 df-ne mpbi eqeq1 mtbiri ndmfv nsyl2 adantr
      orcd prid2 nosgnn0i eqcom syl6bb adantl olcd 3jaoi sylbi syl6bi cin dmres
      eleq2i bitri simplbi onelss sseld onelon sylan intss1 ontri1 syl5ib con2d
      elin jaoi com23 mpd fveq2 eqeq12d necon3bid elrab simplbi2 con2bii sylibr
      weq con3d fvres biimpd r19.21aiva fvresval ori eqcomd eqeq2 mpbid a1i cpr
      ad2antrl crn wfun nofun fvelrn norn syld nosgnn0 eleq1 nsyli adantrl jcad
      anim12i ad2antll adantrr 3orim123d 3imtr4g sylbid breq12d anbi12d rcla4ev
      syl6 raleq syl12anc sltval 3adant3 mpbird ) AUCFZBUCFZCGFZUBZACUDZBCUDZUE
      HZABUEHZUURUVAIZUVBDUFZAJZUVDBJZKZDUAUFZUGZUVHAJZUVHBJZLMUHLNUHMNUHUIZHZI
      ZUAGUJZUVCEUFZUUSJZUVPUUTJZUKZEGULZUMZGFZUVGDUWAUGZUWAAJZUWABJZUVLHZUVOUU
      RUVAUWBUURUUSUCFZUUTUCFZUVAUWBOUUOUUQUWGUUPACUNUOZUUPUUQUWHUUOBCUNUPZUWGU
      WHIUVAUWAUQFUWBUUSUUTEURUVSEUSUTVAPZUVCUVGDUWAUVCUVDUWAFZIZUVDCFZUVDUUSJZ
      UVDUUTJZKZUVGUVCUWLUWNUVCUWACUVDUVCUUQUWACFZUWACVBUUOUUPUUQUVAVCUVCUWAUUS
      VDZFZUWAUUTVDZFZVEZUWRUURUVAUXCUURUVAUWAUUSJZUWAUUTJZUVLHZUXCUURUWGUWHUVA
      UXFVFUWIUWJUUSUUTEVGVAZUXFUXDLKZUXEMKZIZUXHUXENKZIZUXDMKZUXKIZVHZUXCLMLNM
      NUXDUXEUWAUUSVIUWAUUTVIVJNGVKVLZUXPVMZUXJUXCUXLUXNUXJUWTUXBUXHUWTUXIUXHUX
      MUWTUXHUXMLMKZLMUKUXRQVNLMVOVPUXDLMVQVRZUWAUUSVSVTZWAWBUXLUWTUXBUXHUWTUXK
      UXTWAWBUXNUXBUWTUXKUXBUXMUXKUXIUXBUXKUXIMNKZMNUKUYAQNLNUXPWCWDMNVOVPUXKUX
      INMKUYAUXENMVQNMWEWFVRZUWAUUTVSVTZWGWHWIWJWKPUWTUWRUXBUWTUWRUWAAVDZFZUWTU
      WACUYDWLZFUWRUYEIUWSUYFUWAACWMWNUWACUYDXEWOZWPZUXBUWRUWABVDZFZUXBUWACUYIW
      LZFUWRUYJIUXAUYKUWABCWMWNUWACUYIXEWOZWPZXFRCUWAWQSWRPUWMUWOUWPUKZQZUWQUWM
      UVDGFZUVDUVTFZQZUYOUVCUWBUWLUYPUWKUWAUVDWSWTZUWMUYPUYRUYSUVCUWLUYPUYROUVC
      UYPUWLUYRUVCUYPUWLUYROZUVCUWBUYPUYTUWKUWBUYPIZUYQUWLUYQUWAUVDVBVUAUWLQUVD
      UVTXAUWAUVDXBXCXDWTTXGPXHUYPUYNUYQUYQUYPUYNUVSUYNEUVDGEDXPZUVQUVRUWOUWPVU
      BUVQUWOUVRUWPUVPUVDUUSXIUVPUVDUUTXIXJXKXLXMXQSUYNUWQUWOUWPVOXNXOUWNUWQUVG
      UWNUWOUVEUWPUVFUVDCAXRUVDCBXRXJXSSXTUURUVAUWFUURUVAUXFUWFUXGUURUXOUWDLKZU
      WEMKZIZVUCUWENKZIZUWDMKZVUFIZVHUXFUWFUURUXJVUEUXLVUGUXNVUIUURUXJVUCVUDUXJ
      VUCOUURUXHVUCUXIUXHUWDUXDKVUCUXHUXDUWDUXHUXMUXDUWDKZUXSVUJUXMUWACAYAYBVTY
      CUXDLUWDYDYEZWAYFUURUXJVUDUURUXJIZUYJQZVUDVULUWRUXBQZVUMVULUWTUWRUXHUWTUU
      RUXIUXTYHUYHRUURUXIVUNUXHUURUXIVUNUURUWHUXIVUNOUWJUWHUXBUXELNYGZFZUXIUWHU
      XBUXEUUTYIZFZVUPUWHUUTYJZUXBVUROUUTYKVUSUXBVURUWAUUTYLTRUWHVUQVUOUXEUUTYM
      WRYNUXIVUPMVUOFZYOUXEMVUOYPVRYQRPYRUWRUYJUXBUXBUWRUYJUYLXMXQSUWABVSRTYSUX
      LVUGOUURUXHVUCUXKVUFVUKUXKUWEUXEKVUFUXKUXEUWEUXKUXIUXEUWEKZUYBVVAUXIUWACB
      YAYBVTYCUXENUWEYDYEZYTYFUURUXNVUHVUFUURUXNUYEQZVUHUURUXNVVCUURUXNIZUWRUWT
      QZVVCVVDUXBUWRUXKUXBUURUXMUYCUUAUYMRUURUXMVVEUXKUURUXMVVEUURUWGUXMVVEOUWI
      UWGUWTUXDVUOFZUXMUWGUWTUXDUUSYIZFZVVFUWGUUSYJZUWTVVHOUUSYKVVIUWTVVHUWAUUS
      YLTRUWGVVGVUOUXDUUSYMWRYNUXMVVFVUTYOUXDMVUOYPVRYQRPUUBUWRUYEUWTUWTUWRUYEU
      YGXMXQSTUWAAVSUUIUXNVUFOUURUXKVUFUXMVVBWGYFYSUUCUXQLMLNMNUWDUWEUWAAVIUWAB
      VIVJUXPUXPVMUUDUUEPUVNUWCUWFIUAUWAGUVHUWAKZUVIUWCUVMUWFUVGDUVHUWAUUJVVJUV
      JUWDUVKUWEUVLUVHUWAAXIUVHUWABXIUUFUUGUUHUUKUURUVBUVOVFZUVAUUOUUPVVKUUQUAD
      ABUULUUMWAUUNT $.
      $( [9-Mar-2012] $) $( [4-Sep-2011] $)
  $}


  ${
    $d A x $.  $d X x $.
    noxpsgn.1 $e |- X e. { 1o , 2o } $.
    $( The cross product of an ordinal and the singleton of a sign is a
       surreal.  (Contributed by Scott Fenton, 21-Jun-2011.) $)
    noxpsgn $p |- ( A e. On -> ( A X. { X } ) e. No ) $=
      ( vx con0 wcel cv c1o c2o cpr csn cxp wf wrex csur wss elisseti snss mpbi
      fconst fss mp2an feq2 rcla4ev mpan2 elno sylibr ) AEFZDGZHIJZABKZLZMZDENZ
      ULOFUHAUJULMZUNAUKULMUKUJPZUOABBUJCQZTBUJFUPCBUJUQRSAUKUJULUAUBUMUODAEUIA
      UJULUCUDUEDULUFUG $.
      $( [10-Jul-2011] $) $( [21-Jun-2011] $)
  $}

  $( The cross product of an ordinal and ` { 1o } ` is a surreal.  (Contributed
     by Scott Fenton, 12-Jun-2011.) $)
  noxp1o $p |- ( A e. On -> ( A X. { 1o } ) e. No ) $=
    ( c1o c2o con0 1on elisseti prid1 noxpsgn ) ABBCBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

  $( The cross product of an ordinal and ` { 2o } ` is a surreal.  (Contributed
     by Scott Fenton, 12-Jun-2011.) $)
  noxp2o $p |- ( A e. On -> ( A X. { 2o } ) e. No ) $=
    ( c2o c1o con0 2on elisseti prid2 noxpsgn ) ABCBBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

