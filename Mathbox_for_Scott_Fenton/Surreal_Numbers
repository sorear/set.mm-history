$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Set up the new syntax for surreal numbers $)
  $c No <s bday $.
  $( PLEASE PUT DESCRIPTION HERE. $)
  csur $a class No $.
  $( PLEASE PUT DESCRIPTION HERE. $)
  cslt $a class <s $.
  $( PLEASE PUT DESCRIPTION HERE. $)
  cbday $a class bday $.

  ${
    $d f a $.
    $( Define the class of surreal numbers.  The surreal numbers are a proper
       class of numbers developed by John H. Conway and introduced by Donald
       Knuth in 1975.  They form a proper class into which all ordered fields
       can be embedded.  The approach we take to defining them was first
       introduced by Hary Goshnor, and is based on the conception of a "sign
       expansion" of a surreal number.  We define the surreals as
       ordinal-indexed sequences of ` 1o ` and ` 2o ` , analagous to Goshnor's
       ` ( - ) ` and ` ( + ) ` .

       After introducing this definition, we will abstract away from it using
       axioms that Norman Alling developed in "Foundations of Analysis over
       Surreal Number Fields."  This is done in a effort to be agnostic towards
       the exact implementation of surreals. $)
    df-no $a |- No = { f | E. a e. On f : a --> { 1o , 2o } } $.
  $}

  ${
    $d f g x y $.
    $( Next, we introduce surreal less-than, a comparison relationship over the
       surreals by lexicographically ordering them. $)
    df-slt $a |- <s = { <. f , g >. | ( ( f e. No /\ g e. No ) /\
                         E. x e. On (
                            A. y e. x ( f ` y ) = ( g ` y ) /\
                              ( f ` x ) { <. 1o , (/) >. ,
                                          <. 1o , 2o >. ,
                                          <. (/) , 2o >. } ( g ` x ) ) ) } $.
  $}

  $( Finally, we introduce the birthday function.  This function maps each
     surreal to an ordinal.  In our implementation, this is the domain of the
     sign function.  The important properties of this function are established
     later. $)
  df-bday $a |- bday = ( x e. No |-> dom x ) $.


  ${
    $d A f x $.
    $( Membership in the surreals. $)
    elno $p |- ( A e. No <-> E. x e. On A : x --> { 1o , 2o } ) $=
      ( vf csur wcel cvv cv c1o c2o cpr wf con0 wrex elisset wi visset fex
      mpan2 a1i r19.23aiv wceq feq1 rexbidv df-no elab2g pm5.21nii ) BDEBFEZAGZ
      HIJZBKZALMZBDNUJUGALUJUGOUHLEUJUHFEUGAPUHUIFBQRSTUHUICGZKZALMUKCBDFULBUAU
      MUJALUHUIULBUBUCCAUDUEUF $.
      $( [11-Jun-2011] $)
  $}

  ${
    $d A f g x y $.  $d B f g x y $.
    $( The value of the surreal less than relationship. $)
    sltval $p |- ( ( A e. No /\ B e. No ) ->
            ( A <s B <-> E. x e. On ( A. y e. x ( A ` y ) = ( B ` y ) /\
                         ( A ` x ) { <. 1o , (/) >. , <. 1o , 2o >. ,
                                     <. (/) , 2o >. } ( B ` x ) ) ) ) $=
      ( vf vg csur wcel wa cslt wbr cv cfv wceq wral c1o c0 cop c2o ctp con0
      wrex eleq1 anbi1d fveq1 eqeq1d ralbidv breq1d anbi12d rexbidv anbi2d
      eqeq2d breq2d df-slt brabg bianabs ) CGHZDGHZIZCDJKBLZCMZUTDMZNZBALZOZVDC
      MZVDDMZPQRPSRQSRTZKZIZAUAUBZELZGHZFLZGHZIZUTVLMZUTVNMZNZBVDOZVDVLMZVDVNMZ
      VHKZIZAUAUBZIUQVOIZVAVRNZBVDOZVFWBVHKZIZAUAUBZIUSVKIEFCDGGJVLCNZVPWFWEWKW
      LVMUQVOVLCGUCUDWLWDWJAUAWLVTWHWCWIWLVSWGBVDWLVQVAVRUTVLCUEUFUGWLWAVFWBVHV
      DVLCUEUHUIUJUIVNDNZWFUSWKVKWMVOURUQVNDGUCUKWMWJVJAUAWMWHVEWIVIWMWGVCBVDWM
      VRVBVAUTVNDUEULUGWMWBVGVFVHVDVNDUEUMUIUJUIABEFUNUOUP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( The value of the birthday function within the surreals. $)
    bdayval $p |- ( A e. No -> ( bday ` A ) = dom A ) $=
      ( vx csur wcel cdm cvv cbday cfv wceq dmexg cv dmeq df-bday mptg mpdan )
      ACDAEZFDAGHPIACJBABKZEPCFGQALBMNO $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.  $d X x $.
    noxpsgn.1 $e |- X e. { 1o , 2o } $.
    $( The cross product of an ordinal and the singleton of a sign is a
       surreal. $)
    noxpsgn $p |- ( A e. On -> ( A X. { X } ) e. No ) $=
      ( vx con0 wcel cv c1o c2o cpr csn cxp wf wrex csur wss elisseti fconst
      snss mpbi fss mp2an feq2 rcla4ev mpan2 elno sylibr ) AEFZDGZHIJZABKZLZMZD
      ENZULOFUHAUJULMZUNAUKULMUKUJPZUOABBUJCQZRBUJFUPCBUJUQSTAUKUJULUAUBUMUODAE
      UIAUJULUCUDUEDULUFUG $.
      $( [10-Jul-2011] $) $( [21-Jun-2011] $)
  $}

  $( The cross product of an ordinal and ` { 1o } ` is a surreal. $)
  noxp1o $p |- ( A e. On -> ( A X. { 1o } ) e. No ) $=
    ( c1o c2o con0 1on elisseti prid1 noxpsgn ) ABBCBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

  $( The cross product of an ordinal and ` { 2o } ` is a surreal. $)
  noxp2o $p |- ( A e. On -> ( A X. { 2o } ) e. No ) $=
    ( c2o c1o con0 2on elisseti prid2 noxpsgn ) ABCBBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

  ${
    $d A x $.
    $( A surreal is a function. $)
    nofun $p |- ( A e. No -> Fun A ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex wfun elno wi ffun a1i
      r19.23aiv sylbi ) ACDBEZFGHZAIZBJKALZBAMUAUBBJUAUBNSJDSTAOPQR $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The domain of a surreal is an ordinal. $)
    nodmon $p |- ( A e. No -> dom A e. On ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex cdm elno fdm eleq1d biimprcd
      r19.23aiv sylbi ) ACDBEZFGHZAIZBJKALZJDZBAMUAUCBJUAUCSJDUAUBSJSTANOPQR $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The range of a surreal is a subset of the surreal signs. $)
    norn $p |- ( A e. No -> ran A C_ { 1o , 2o } ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex crn wss elno wi frn a1i
      r19.23aiv sylbi ) ACDBEZFGHZAIZBJKALUAMZBANUBUCBJUBUCOTJDTUAAPQRS $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  $( The domain of a surreal has the ordinal property. $)
  nodmord $p |- ( A e. No -> Ord dom A ) $=
    ( csur wcel cdm con0 word nodmon eloni syl ) ABCADZECJFAGJHI $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  ${
    $d A a x y $.  $d B a x y $.
    $( Alternate expression for surreal less than.  Two surreals obey surreal
       less than iff they obey the sign ordering at the first place they
       differ. $)
    sltval2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B <->
                       ( A ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                       { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                       ( B ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                     ) ) $=
      ( vy vx csur wcel wa cslt wbr cv cfv wceq wral c1o c0 cop c2o ctp con0
      wrex wne crab cint sltval cvv w3o fvex 0ex 2on elisseti brtp wn 1n0
      df-ne mpbi eqtr2 ex mtoi fvprc nsyl2 adantr 2on0 adantl 3jaoi sylbi
      onintrab sylib wi onelon syl wss sylan ontri1 con2bid biimpd com23 imp
      mpd intss1 nsyl weq fveq2 neeq1d neeq2d bitrd elrab notbii syl6ib imnan
      syl6ibr mpdd con2bii r19.21aiv pm3.27 jca32 raleq1 breq12d anbi12d
      rcla4ev syl6 biimpri adantlr onint ssrab2 ne0i sylancr hbrab1 hbint
      ax-17 hbfv hbne elrabf pm3.27bi eqeq12d rcla4cv ad2antlr mtod wb simpll
      oninton sylanc mpbird eqssd syldan eqeq1 biimparc syl5 expdimp csuc 1on
      0elon suc11 mp2an nemtbir df-2o df-1o eqeq12i mtbir expcom eqeq2 biimpa
      3imtr4i sylan2 fveq2d pm2.43d expimpd r19.23aiv impbid1 bitr4d ) AFGBFGHZ
      ABIJDKZALZUULBLZMZDEKZNZUUPALZUUPBLZOPQORQPRQSZJZHZETUAZCKZALZUVDBLZUBZCT
      UCZUDZALZUVIBLZUUTJZEDABUEUUKUVLUVCUUKUVLUVITGZUUODUVINZUVLHZHZUVCUUKUVLU
      VPUUKUVLHZUVMUVNUVLUVLUVMUUKUVLUVIUFGZUVMUVLUVJOMZUVKPMZHZUVSUVKRMZHZUVJP
      MZUWBHZUGUVROPORPRUVJUVKUVIAUHUVIBUHUIRTUJUKZUWFULUWAUVRUWCUWEUVSUVRUVTUV
      SUWDUVRUVSUWDOPMZOPUBUWGUMUNOPUOUPZUVSUWDUWGUVJOPUQURUSUVIAUTVAZVBUVSUVRU
      WBUWIVBUWBUVRUWDUWBUVTUVRUWBUVTRPMZRPUBUWJUMVCRPUOUPZUWBUVTUWJUVKRPUQURUS
      UVIBUTVAVDVEVFUVGCVGVHVDZUVQUUODUVIUVQUULUVIGZUUMUUNUBZUMZUUOUVQUWMUULTGZ
      UWOUVQUVMUWMUWPVIUWLUVMUWMUWPUVIUULVJZURVKUVQUWMUWPUWNHZUMZUWPUWOVIUVQUWM
      UULUVHGZUMZUWSUVQUWMUXAUVQUWMHZUVIUULVLZUWTUXBUWPUXCUMZUVMUWMUWPUVQUWQUWL
      VMUVQUWMUWPUXDVIZUVQUVMUWMUXEVIUWLUVMUWPUWMUXDUVMUWPUWMUXDVIUVMUWPHZUWMUX
      DUXFUXCUWMUVIUULVNVOVPURVQVKVRVSUULUVHVTWAURUWTUWRUVGUWNCUULTCDWBZUVGUUMU
      VFUBUWNUXGUVEUUMUVFUVDUULAWCWDUXGUVFUUNUUMUVDUULBWCWEWFWGWHWIUWPUWNWJWKWL
      UWNUUOUUMUUNUOWMWKWNUUKUVLWOWPURUVBUVOEUVITUUPUVIMZUUQUVNUVAUVLUUODUUPUVI
      WQUXHUURUVJUUSUVKUUTUUPUVIAWCUUPUVIBWCWRWSWTXAUVBUVLETUUPTGZUUQUVAUVLUXIU
      UQHZUVAUVLUXJUVAUVAUVLVIUXJUVAHZUVAUVLUXKUURUVJUUSUVKUUTUXKUUPUVIAUXJUURU
      USUBZUXHUVAUXJUXLUUPUVHGZUXHUXIUXLUXMUUQUXMUXIUXLHUVGUXLCUUPTCEWBZUVGUURU
      VFUBUXLUXNUVEUURUVFUVDUUPAWCWDUXNUVFUUSUURUVDUUPBWCWEWFWGXBXCUXJUXMHZUUPU
      VIUXOUUPUVIVLZUVIUUPGZUMZUXOUXQUVJUVKMZUXOUVJUVKUBZUXSUMUXOUVIUVHGZUXTUVH
      TVLZUVHPUBZUYAUXOUVHXDUVGCTXEZUXMUYCUXJUVHUUPXFZVDXGUYAUVMUXTUVGUXTCEUVIT
      CEUVHUVGCETXHXIZUXICXJCEUVJUVKCEUVIAUUPAGCXJUYFXKCEUVIBUUPBGCXJUYFXKXLUVD
      UVIMZUVGUVJUVFUBUXTUYGUVEUVJUVFUVDUVIAWCWDUYGUVFUVKUVJUVDUVIBWCWEWFXMXNVK
      UVJUVKUOVHUUQUXQUXSVIUXIUXMUUOUXSDUVIUUPUULUVIMUUMUVJUUNUVKUULUVIAWCUULUV
      IBWCXOXPXQXRUXIUVMUXPUXRXSUXOUUPUVIVNUXIUUQUXMXTUXMUVMUXJUYBUYCUVMUXMUVHY
      AUYDUYEXGVDYBYCUXMUVIUUPVLUXJUUPUVHVTVDYDYEUUROMZUUSPMZHZUYHUUSRMZHZUURPM
      ZUYKHZUGUURUUSMZUMZUVAUXLUYJUYPUYLUYNUYJUYOUWGUWHUYHUYIUYOUWGUYHUYMUWGUYI
      UYOHUYHUYMUWGUUROPUQURUYOUYMUYIUURUUSPYFYGYHYIUSUYLUYOROMZUYQOYJZPYJZMZUY
      TOPUNOTGPTGUYTUWGXSYKYLOPYMYNYORUYROUYSYPYQYRYSUYHUYKUYOUYQUYHUURRMZUYQUY
      KUYOHZVUAUYHUYQUURROUQYTUYKUYOVUAUUSRUURUUAUUBZYHYIUSUYNUYOUWJUWKUYMUYKUY
      OUWJUYMVUAUWJVUBVUAUYMUWJUURRPUQYTVUCYHYIUSVEOPORPRUURUUSUUPAUHUUPBUHUIUW
      FUWFULUURUUSUOUUCUUDZUUEUXKUUPUVIBVUDUUEWRVPURUUFUUGUUHUUIUUJ $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $( The function value of a surreal is either a sign or the empty set. $)
    nofv $p |- ( A e. No -> ( ( A ` X ) = (/) \/ ( A ` X ) = 1o \/
                               ( A ` X ) = 2o ) ) $=
      ( csur wcel cfv c0 wceq c1o c2o wo w3o cdm wn pm2.1 wi ndmfv a1i wfun
      crn cpr wss wa ssel fvelrn syl5com ex com23 imp con0 1on elisseti 2on
      elpr2 syl6ib nofun norn sylanc orim12d mpi 3orass sylibr ) ACDZBAEZFGZVCH
      GZVCIGZJZJZVDVEVFKVBBALDZMZVIJVHVINVBVJVDVIVGVJVDOVBBAPQARZASZHITZUAZVIVG
      OVBVKVNUBVIVCVMDZVGVKVNVIVOOVKVIVNVOVKVIVNVOOVNVCVLDVOVKVIUBVLVMVCUCBAUDU
      EUFUGUHVCHIHUIUJUKIUIULUKUMUNAUOAUPUQURUSVDVEVFUTVA $.
      $( [10-Jul-2011] $) $( [22-Jun-2011] $)
  $}

