$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Set up the new syntax for surreal numbers $)
  $c No <s bday $.
  $( Declare the class of all surreal numbers (see ~ df-no ). $)
  csur $a class No $.
  $( Declare the less than relationship over surreal numbers (see
     ~ df-slt ). $)
  cslt $a class <s $.
  $( Declare the birthday function for surreal numbers (see ~ df-bday ). $)
  cbday $a class bday $.

  ${
    $d f a $.
    $( Define the class of surreal numbers.  The surreal numbers are a proper
       class of numbers developed by John H. Conway and introduced by Donald
       Knuth in 1975.  They form a proper class into which all ordered fields
       can be embedded.  The approach we take to defining them was first
       introduced by Hary Goshnor, and is based on the conception of a "sign
       expansion" of a surreal number.  We define the surreals as
       ordinal-indexed sequences of ` 1o ` and ` 2o ` , analagous to Goshnor's
       ` ( - ) ` and ` ( + ) ` .

       After introducing this definition, we will abstract away from it using
       axioms that Norman Alling developed in "Foundations of Analysis over
       Surreal Number Fields."  This is done in a effort to be agnostic towards
       the exact implementation of surreals. $)
    df-no $a |- No = { f | E. a e. On f : a --> { 1o , 2o } } $.
  $}

  ${
    $d f g x y $.
    $( Next, we introduce surreal less-than, a comparison relationship over the
       surreals by lexicographically ordering them. $)
    df-slt $a |- <s = { <. f , g >. | ( ( f e. No /\ g e. No ) /\
                         E. x e. On (
                            A. y e. x ( f ` y ) = ( g ` y ) /\
                              ( f ` x ) { <. 1o , (/) >. ,
                                          <. 1o , 2o >. ,
                                          <. (/) , 2o >. } ( g ` x ) ) ) } $.
  $}

  $( Finally, we introduce the birthday function.  This function maps each
     surreal to an ordinal.  In our implementation, this is the domain of the
     sign function.  The important properties of this function are established
     later. $)
  df-bday $a |- bday = ( x e. No |-> dom x ) $.


  ${
    $d A f x $.
    $( Membership in the surreals.  (Shortened proof on 2012-Apr-14, SF). $)
    elno $p |- ( A e. No <-> E. x e. On A : x --> { 1o , 2o } ) $=
      ( vf csur wcel cvv cv c1o c2o cpr wf con0 wrex elisset fex ancoms
      r19.23aiva wceq feq1 rexbidv df-no elab2g pm5.21nii ) BDEBFEZAGZHIJZBKZAL
      MZBDNUGUDALUGUELEUDUEUFLBOPQUEUFCGZKZALMUHCBDFUIBRUJUGALUEUFUIBSTCAUAUBUC
      $.
      $( [12-May-2012] $) $( [11-Jun-2011] $)
  $}

  ${
    $d A f g x y $.  $d B f g x y $.
    $( The value of the surreal less than relationship. $)
    sltval $p |- ( ( A e. No /\ B e. No ) ->
            ( A <s B <-> E. x e. On ( A. y e. x ( A ` y ) = ( B ` y ) /\
                         ( A ` x ) { <. 1o , (/) >. , <. 1o , 2o >. ,
                                     <. (/) , 2o >. } ( B ` x ) ) ) ) $=
      ( vf vg csur wcel wa cslt wbr cv cfv wceq wral c1o c0 cop c2o ctp con0
      wrex eleq1 anbi1d fveq1 eqeq1d ralbidv breq1d anbi12d rexbidv anbi2d
      eqeq2d breq2d df-slt brabg bianabs ) CGHZDGHZIZCDJKBLZCMZUTDMZNZBALZOZVDC
      MZVDDMZPQRPSRQSRTZKZIZAUAUBZELZGHZFLZGHZIZUTVLMZUTVNMZNZBVDOZVDVLMZVDVNMZ
      VHKZIZAUAUBZIUQVOIZVAVRNZBVDOZVFWBVHKZIZAUAUBZIUSVKIEFCDGGJVLCNZVPWFWEWKW
      LVMUQVOVLCGUCUDWLWDWJAUAWLVTWHWCWIWLVSWGBVDWLVQVAVRUTVLCUEUFUGWLWAVFWBVHV
      DVLCUEUHUIUJUIVNDNZWFUSWKVKWMVOURUQVNDGUCUKWMWJVJAUAWMWHVEWIVIWMWGVCBVDWM
      VRVBVAUTVNDUEULUGWMWBVGVFVHVDVNDUEUMUIUJUIABEFUNUOUP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( The value of the birthday function within the surreals. $)
    bdayval $p |- ( A e. No -> ( bday ` A ) = dom A ) $=
      ( vx csur wcel cdm cvv cbday cfv wceq dmexg cv dmeq df-bday fvmptg mpdan
      ) ACDAEZFDAGHPIACJBABKZEPCFGQALBMNO $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( A surreal is a function. $)
    nofun $p |- ( A e. No -> Fun A ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex wfun elno ffun r19.23aivw
      sylbi ) ACDBEZFGHZAIZBJKALZBAMSTBJQRANOP $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The domain of a surreal is an ordinal. $)
    nodmon $p |- ( A e. No -> dom A e. On ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex cdm elno fdm eleq1d biimprcd
      r19.23aiv sylbi ) ACDBEZFGHZAIZBJKALZJDZBAMUAUCBJUAUCSJDUAUBSJSTANOPQR $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)

    $( The range of a surreal is a subset of the surreal signs. $)
    norn $p |- ( A e. No -> ran A C_ { 1o , 2o } ) $=
      ( vx csur wcel cv c1o c2o cpr wf con0 wrex crn wss elno frn r19.23aivw
      sylbi ) ACDBEZFGHZAIZBJKALSMZBANTUABJRSAOPQ $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  $( The domain of a surreal has the ordinal property. $)
  nodmord $p |- ( A e. No -> Ord dom A ) $=
    ( csur wcel cdm con0 word nodmon eloni syl ) ABCADZECJFAGJHI $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)


  ${
    $d x A $.
    $( An alternative condition for membership in ` No ` . $)
    elno2 $p |- ( A e. No <->
              ( Fun A /\ dom A e. On /\ ran A C_ { 1o , 2o } ) ) $=
      ( vx csur wcel wfun cdm con0 crn c1o c2o cpr wss w3a nofun nodmon norn
      3jca cv wf wrex simp2 wfn wa wceq df-fn simpl eqidd sylanbrc anim1i
      3impa df-f sylibr feq2 rcla4ev syl11anc elno impbii ) ACDZAEZAFZGDZAHIJKZ
      LZMZURUSVAVCANAOAPQVDBRZVBASZBGTZURVDVAUTVBASZVGUSVAVCUAVDAUTUBZVCUCZVHUS
      VAVCVJUSVAUCZVIVCVIUSUTUTUDVKAUTUEUSVAUFVKUTUGUHUIUJUTVBAUKULVFVHBUTGVEUT
      VBAUMUNUOBAUPULUQ $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  $( Another condition for membership in ` No ` . $)
  elno3 $p |- ( A e. No <-> ( A : dom A --> { 1o , 2o } /\ dom A e. On ) ) $=
    ( wfun cdm con0 wcel crn c1o c2o cpr wss w3a wa csur wf 3ancomb df-3an
    bitri elno2 wfn df-f funfn anbi1i bitr4i 3bitr4i ) ABZACZDEZAFGHIZJZKZUEUIL
    ZUGLZAMEUFUHANZUGLUJUEUIUGKULUEUGUIOUEUIUGPQARUMUKUGUMAUFSZUILUKUFUHATUEUNU
    IAUAUBUCUBUD $.
    $( [12-May-2012] $) $( [14-Apr-2012] $)

  ${
    $d A a x y $.  $d B a x y $.
    $( Alternate expression for surreal less than.  Two surreals obey surreal
       less than iff they obey the sign ordering at the first place they
       differ. $)
    sltval2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B <->
                       ( A ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                       { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                       ( B ` |^| { a e. On | ( A ` a ) =/= ( B ` a ) } )
                     ) ) $=
      ( vy vx csur wcel wa cslt wbr cv cfv wceq wral c1o c0 cop c2o ctp con0
      wrex wne crab cint sltval cvv w3o fvex 0ex 2on elisseti brtp wn 1n0
      df-ne mpbi eqeq1 mtbiri fvprc nsyl2 adantr 2on0 adantl 3jaoi sylbi
      onintrab sylib onelon expcom syl5 weq fveq2 eqeq12d necon3bid onnminsb
      com12 syld con2bii syl6ibr r19.21aiv jca ex impac anass raleq breq12d
      anbi12d rcla4ev syl wi elrab biimpri adantlr wss onint ssrab2 ne0i
      sylancr hbrab1 hbint ax-17 hbfv hbne elrabf simprbi rcla4cv ad2antlr
      mtod wb simpll oninton ontri1 syl11anc mpbird intss1 eqssd syldan eqeq12
      csuc 1on 0elon suc11 mp2an mpbir df-2o df-1o eqeq12i nemtbir eqcom
      syl6bb mtbi 3imtr4i sylan2 fveq2d biimpd pm2.43d expimpd r19.23aiv
      impbid1 bitr4d ) AFGBFGHZABIJDKZALZUUBBLZMZDEKZNZUUFALZUUFBLZOPQORQPRQSZJ
      ZHZETUAZCKZALZUUNBLZUBZCTUCZUDZALZUUSBLZUUJJZEDABUEUUAUVBUUMUUAUVBUUMUUAU
      VBHZUUSTGZUUEDUUSNZUVBHZHZUUMUVCUVDUVEHZUVBHUVGUUAUVBUVHUUAUVBUVHUVCUVDUV
      EUVBUVDUUAUVBUUSUFGZUVDUVBUUTOMZUVAPMZHZUVJUVARMZHZUUTPMZUVMHZUGUVIOPORPR
      UUTUVAUUSAUHUUSBUHUIRTUJUKZUVQULUVLUVIUVNUVPUVJUVIUVKUVJUVOUVIUVJUVOOPMZO
      PUBZUVRUMUNOPUOUPZUUTOPUQURUUSAUSUTZVAUVJUVIUVMUWAVAUVMUVIUVOUVMUVKUVIUVM
      UVKRPMZRPUBUWBUMVBRPUOUPZUVARPUQURUUSBUSUTVCVDVEUUQCVFVGVCZUVCUUEDUUSUVCU
      UBUUSGZUUCUUDUBZUMZUUEUWEUVCUWGUWEUVCUUBTGZUWGUWEUVDUWHUVCUVDUWEUWHUUSUUB
      VHVIUWDVJUWHUWEUWGUUQUWFCUUBCDVKZUUOUUPUUCUUDUWIUUOUUCUUPUUDUUNUUBAVLUUNU
      UBBVLVMVNVOVPVQVPUWFUUEUUCUUDUOVRVSVTWAWBWCUVDUVEUVBWDVGUULUVFEUUSTUUFUUS
      MZUUGUVEUUKUVBUUEDUUFUUSWEUWJUUHUUTUUIUVAUUJUUFUUSAVLUUFUUSBVLWFWGWHWIWBU
      ULUVBETUUFTGZUUGUUKUVBUWKUUGHZUUKUVBUWLUUKUUKUVBWJUWLUUKHZUUKUVBUWMUUHUUT
      UUIUVAUUJUWMUUFUUSAUWLUUHUUIUBZUWJUUKUWLUWNUUFUURGZUWJUWKUWNUWOUUGUWOUWKU
      WNHUUQUWNCUUFTCEVKZUUOUUPUUHUUIUWPUUOUUHUUPUUIUUNUUFAVLUUNUUFBVLVMVNWKWLW
      MUWLUWOHZUUFUUSUWQUUFUUSWNZUUSUUFGZUMZUWQUWSUUTUVAMZUWQUUTUVAUBZUXAUMUWQU
      USUURGZUXBUURTWNZUURPUBZUXCUWQUURWOUUQCTWPZUWOUXEUWLUURUUFWQZVCWRUXCUVDUX
      BUUQUXBCEUUSTCEUURUUQCETWSWTZUWKCXACEUUTUVACEUUSAUUFAGCXAUXHXBCEUUSBUUFBG
      CXAUXHXBXCUUNUUSMZUUOUUPUUTUVAUXIUUOUUTUUPUVAUUNUUSAVLUUNUUSBVLVMVNXDXEWI
      UUTUVAUOVGUUGUWSUXAWJUWKUWOUUEUXADUUSUUFUUBUUSMUUCUUTUUDUVAUUBUUSAVLUUBUU
      SBVLVMXFXGXHUWQUWKUVDUWRUWTXIUWKUUGUWOXJUWOUVDUWLUXDUXEUVDUWOUURXKUXFUXGW
      RVCUUFUUSXLXMXNUWOUUSUUFWNUWLUUFUURXOVCXPXQUUHOMZUUIPMHZUXJUUIRMZHZUUHPMU
      XLHZUGUUHUUIMZUMZUUKUWNUXKUXPUXMUXNUXKUXOUVRUVTUUHOUUIPXRURUXMUXOROMZUXQO
      XSZPXSZUXRUXSUBZUVSUNOTGZPTGZUXTUVSXIXTYAUYAUYBHUXRUXSOPOPYBVNYCYDRUXROUX
      SYEYFYGYHUXMUXOORMUXQUUHOUUIRXRORYIYJURUXNUXOPRMZUWBUYCUWCRPYIYKUUHPUUIRX
      RURVDOPORPRUUHUUIUUFAUHUUFBUHUIUVQUVQULUUHUUIUOYLYMZYNUWMUUFUUSBUYDYNWFYO
      WBYPYQYRYSYT $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}

  ${
    $( The function value of a surreal is either a sign or the empty set. $)
    nofv $p |- ( A e. No -> ( ( A ` X ) = (/) \/ ( A ` X ) = 1o \/
                               ( A ` X ) = 2o ) ) $=
      ( csur wcel cfv c0 wceq c1o c2o wo w3o cdm wn pm2.1 wi ndmfv a1i wfun
      crn cpr wss nofun norn wa ssel fvelrn syl5com ex com23 imp con0 1on
      elisseti 2on elpr2 syl6ib syl11anc orim12d mpi 3orass sylibr ) ACDZBAEZFG
      ZVCHGZVCIGZJZJZVDVEVFKVBBALDZMZVIJVHVINVBVJVDVIVGVJVDOVBBAPQVBARZASZHITZU
      AZVIVGOAUBAUCVKVNUDVIVCVMDZVGVKVNVIVOOVKVIVNVOVKVIVNVOOVNVCVLDVOVKVIUDVLV
      MVCUEBAUFUGUHUIUJVCHIHUKULUMIUKUNUMUOUPUQURUSVDVEVFUTVA $.
      $( [10-Jul-2011] $) $( [22-Jun-2011] $)
  $}

  ${
    $( ` (/) ` is not a surreal sign. $)
    nosgnn0 $p |- -. (/) e. { 1o , 2o } $=
      ( c0 c1o c2o cpr wcel wceq wo wne wn 1n0 necom df-ne bitri mpbi csuc
      nsuceq0 df-2o neeq2i bitr4i pm3.2ni 0ex elpr mtbir ) ABCDEABFZACFZGUDUEBA
      HZUDIZJUFABHUGBAKABLMNACHZUEIBOZAHZUHBPUJAUIHUHUIAKCUIAQRSNACLNTABCUAUBUC
      $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    nosgnn0i.1 $e |- X e. { 1o , 2o } $.
    $( If ` X ` is a surreal sign, then it is not null. $)
    nosgnn0i $p |- (/) =/= X $=
      ( c0 wne wceq wn c1o c2o cpr wcel nosgnn0 eleq1 mpbiri mto df-ne mpbir )
      CADCAEZFQCGHIZJZKQSARJBCARLMNCAOP $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The restriction of a surreal to an ordinal is still a surreal. $)
    noreson $p |- ( ( A e. No /\ B e. On ) -> ( A |` B ) e. No ) $=
      ( vx vy cv c1o c2o cpr wf con0 wrex wcel wa cres csur wi an32 cin onin
      adantr fresin adantl feq2 rcla4ev syl11anc sylbi ex r19.23aiva imp elno
      anbi1i 3imtr4i ) CEZFGHZAIZCJKZBJLZMDEZUNABNZIZDJKZAOLZUQMUSOLUPUQVAUOUQV
      APCJUMJLZUOMZUQVAVDUQMVCUQMZUOMZVAVCUOUQQVFUMBRZJLZVGUNUSIZVAVEVHUOUMBSTU
      OVIVEUMUNABUAUBUTVIDVGJURVGUNUSUCUDUEUFUGUHUIVBUPUQCAUJUKDUSUJUL $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A k $.  $d B k $.

    $( If ` A <s B ` , then the sign of ` A ` at the first place they differ is
       either undefined or ` 1o ` $)
    sltsgn1 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( A ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 1o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab cint c1o c0 cop c2o ctp
      wceq wo sltval2 w3o fvex 0ex 2on elisseti brtp olc adantr orc 3jaoi
      sylbi syl6bi ) ADEBDEFABGHCIZAJUOBJKCLMNZAJZUPBJZOPQORQPRQSHZUQPTZUQOTZUA
      ZABCUBUSVAURPTZFZVAURRTZFZUTVEFZUCVBOPORPRUQURUPAUDUPBUDUERLUFUGZVHUHVDVB
      VFVGVAVBVCVAUTUIZUJVAVBVEVIUJUTVBVEUTVAUKUJULUMUN $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)

    $( If ` A <s B ` , then the sign of ` B ` at the first place they differ is
       either undefined or ` 2o ` $)
    sltsgn2 $p |- ( ( A e. No /\ B e. No ) ->
                     ( A <s B ->
                  ( ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = (/) \/
                    ( B ` |^| { k e. On | ( A ` k ) =/= ( B ` k ) } ) = 2o )
                    ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab cint c1o c0 cop c2o ctp
      wceq wo sltval2 w3o fvex 0ex 2on elisseti brtp orc adantl olc 3jaoi
      sylbi syl6bi ) ADEBDEFABGHCIZAJUOBJKCLMNZAJZUPBJZOPQORQPRQSHZURPTZURRTZUA
      ZABCUBUSUQOTZUTFZVCVAFZUQPTZVAFZUCVBOPORPRUQURUPAUDUPBUDUERLUFUGZVHUHVDVB
      VEVGUTVBVCUTVAUIUJVAVBVCVAUTUKZUJVAVBVFVIUJULUMUN $.
      $( [10-Sep-2011] $) $( [4-Sep-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( If ` A <s B ` , then the intersection of all the ordinals that have
       differing signs in ` A ` and ` B ` exists. $)
    sltintdifex $p |- ( ( A e. No /\ B e. No ) ->
       ( A <s B -> |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. _V ) ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab cint c1o c0 cop c2o ctp cvv
      sltval2 wceq w3o fvex 0ex 2on elisseti brtp wn fvprc 1n0 df-ne mpbi
      eqeq1 eqcom syl6bb mtbiri syl con4i adantr 2on0 adantl 3jaoi sylbi
      syl6bi ) ADEBDEFABGHCIZAJVFBJKCLMNZAJZVGBJZOPQORQPRQSHZVGTEZABCUAVJVHOUBZ
      VIPUBZFZVLVIRUBZFZVHPUBZVOFZUCVKOPORPRVHVIVGAUDVGBUDUERLUFUGZVSUHVNVKVPVR
      VLVKVMVKVLVKUIZVQVLUIVGAUJVQVLOPUBZOPKWAUIUKOPULUMVQVLPOUBWAVHPOUNPOUOUPU
      QURUSZUTVLVKVOWBUTVOVKVQVKVOVTVMVOUIVGBUJVMVORPUBZRPKWCUIVARPULUMVMVOPRUB
      WCVIPRUNPRUOUPUQURUSVBVCVDVE $.
      $( [22-Mar-2012] $) $( [22-Feb-2012] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.  $d X a x y $.
    $( Lemma for axfe (future) .  If the restrictions of two surreals to a
       given ordinal obey surreal less than, then so do the two surreals
       themselves. $)
    sltres $p |- ( ( A e. No /\ B e. No /\ X e. On ) ->
                      ( ( A |` X ) <s ( B |` X ) -> A <s B ) ) $=
      ( vy vx va csur wcel con0 w3a cres cslt wbr wa cv cfv wceq wral c1o c0
      cop c2o ctp wrex wne crab cint wi noreson 3adant2 3adant1 cvv
      sltintdifex onintrab syl6ib syl11anc imp wss simpl3 cdm wo wb sltval2
      w3o fvex 0ex 2on elisseti brtp wn 1n0 df-ne mpbi eqeq1 mtbiri ndmfv
      nsyl2 adantr orcd prid2 nosgnn0i eqcom syl6bb adantl olcd 3jaoi sylbi
      syl6bi cin dmres eleq2i elin bitri simplbi jaoi syl onelss sylc sseld
      onelon sylan ontri1 intss1 syl5bi con2d ex com23 mpd weq fveq2 eqeq12d
      necon3bid elrab simplbi2 con3d con2bii sylibr fvres biimpd r19.21aiva
      fvresval ori eqcomd eqeq2 mpbid a1i ad2antrl cpr crn wfun nofun fvelrn
      norn syld nosgnn0 eleq1 nsyli adantrl jcad anim12i ad2antll adantrr syl6
      3orim123d 3imtr4g sylbid raleq breq12d anbi12d rcla4ev syl12anc sltval
      3adant3 mpbird ) AGHZBGHZCIHZJZACKZBCKZLMZABLMZUURUVANZUVBDOZAPZUVDBPZQZD
      EOZRZUVHAPZUVHBPZSTUASUBUATUBUAUCZMZNZEIUDZUVCFOZUUSPZUVPUUTPZUEZFIUFZUGZ
      IHZUVGDUWARZUWAAPZUWABPZUVLMZUVOUURUVAUWBUURUUSGHZUUTGHZUVAUWBUHUUOUUQUWG
      UUPACUIUJZUUPUUQUWHUUOBCUIUKZUWGUWHNUVAUWAULHUWBUUSUUTFUMUVSFUNUOUPUQZUVC
      UVGDUWAUVCUVDUWAHZNZUVDCHZUVDUUSPZUVDUUTPZQZUVGUVCUWLUWNUVCUWACUVDUVCUUQU
      WACHZUWACURUUOUUPUUQUVAUSUVCUWAUUSUTZHZUWAUUTUTZHZVAZUWRUURUVAUXCUURUVAUW
      AUUSPZUWAUUTPZUVLMZUXCUURUWGUWHUVAUXFVBUWIUWJUUSUUTFVCUPZUXFUXDSQZUXETQZN
      ZUXHUXEUBQZNZUXDTQZUXKNZVDZUXCSTSUBTUBUXDUXEUWAUUSVEUWAUUTVEVFUBIVGVHZUXP
      VIZUXJUXCUXLUXNUXJUWTUXBUXHUWTUXIUXHUXMUWTUXHUXMSTQZSTUEUXRVJVKSTVLVMUXDS
      TVNVOZUWAUUSVPVQZVRVSUXLUWTUXBUXHUWTUXKUXTVRVSUXNUXBUWTUXKUXBUXMUXKUXIUXB
      UXKUXITUBQZTUBUEUYAVJUBSUBUXPVTWATUBVLVMUXKUXIUBTQUYAUXEUBTVNUBTWBWCVOZUW
      AUUTVPVQZWDWEWFWGWHUQUWTUWRUXBUWTUWRUWAAUTZHZUWTUWACUYDWIZHUWRUYENUWSUYFU
      WAACWJWKUWACUYDWLWMZWNZUXBUWRUWABUTZHZUXBUWACUYIWIZHUWRUYJNUXAUYKUWABCWJW
      KUWACUYIWLWMZWNZWOWPCUWAWQWRWSUQUWMUWOUWPUEZVJZUWQUWMUVDIHZUVDUVTHZVJZUYO
      UWBUWLUYPUVCUWAUVDWTUWKXAZUWMUYPUYRUYSUVCUWLUYPUYRUHUVCUYPUWLUYRUVCUYPUWL
      UYRUHZUWBUYPUYTUVCUWBUYPNZUYQUWLVUAUWAUVDURUWLVJUYQUWAUVDXBUVDUVTXCXDXEUW
      KXAXFXGUQXHUYPUYNUYQUYQUYPUYNUVSUYNFUVDIFDXIZUVQUVRUWOUWPVUBUVQUWOUVRUWPU
      VPUVDUUSXJUVPUVDUUTXJXKXLXMXNXOWRUYNUWQUWOUWPVLXPXQUWNUWQUVGUWNUWOUVEUWPU
      VFUVDCAXRUVDCBXRXKXSWRXTUURUVAUWFUURUVAUXFUWFUXGUURUXOUWDSQZUWETQZNZVUCUW
      EUBQZNZUWDTQZVUFNZVDUXFUWFUURUXJVUEUXLVUGUXNVUIUURUXJVUCVUDUXJVUCUHUURUXH
      VUCUXIUXHUWDUXDQVUCUXHUXDUWDUXHUXMUXDUWDQZUXSVUJUXMUWACAYAYBVQYCUXDSUWDYD
      YEZVRYFUURUXJVUDUURUXJNZUYJVJZVUDVULUWRUXBVJZVUMVULUWTUWRUXHUWTUURUXIUXTY
      GUYHWPUURUXIVUNUXHUURUXIVUNUURUWHUXIVUNUHUWJUWHUXBUXESUBYHZHZUXIUWHUXBUXE
      UUTYIZHZVUPUWHUUTYJZUXBVURUHUUTYKVUSUXBVURUWAUUTYLXFWPUWHVUQVUOUXEUUTYMWS
      YNUXIVUPTVUOHZYOUXETVUOYPVOYQWPUQYRUWRUYJUXBUXBUWRUYJUYLXNXOWRUWABVPWPXFY
      SUXLVUGUHUURUXHVUCUXKVUFVUKUXKUWEUXEQVUFUXKUXEUWEUXKUXIUXEUWEQZUYBVVAUXIU
      WACBYAYBVQYCUXEUBUWEYDYEZYTYFUURUXNVUHVUFUURUXNUYEVJZVUHUURUXNVVCUURUXNNZ
      UWRUWTVJZVVCVVDUXBUWRUXKUXBUURUXMUYCUUAUYMWPUURUXMVVEUXKUURUXMVVEUURUWGUX
      MVVEUHUWIUWGUWTUXDVUOHZUXMUWGUWTUXDUUSYIZHZVVFUWGUUSYJZUWTVVHUHUUSYKVVIUW
      TVVHUWAUUSYLXFWPUWGVVGVUOUXDUUSYMWSYNUXMVVFVUTYOUXDTVUOYPVOYQWPUQUUBUWRUY
      EUWTUWTUWRUYEUYGXNXOWRXFUWAAVPUUCUXNVUFUHUURUXKVUFUXMVVBWDYFYSUUDUXQSTSUB
      TUBUWDUWEUWAAVEUWABVEVFUXPUXPVIUUEUUFUQUVNUWCUWFNEUWAIUVHUWAQZUVIUWCUVMUW
      FUVGDUVHUWAUUGVVJUVJUWDUVKUWEUVLUVHUWAAXJUVHUWABXJUUHUUIUUJUUKUURUVBUVOVB
      ZUVAUUOUUPVVKUUQEDABUULUUMVRUUNXF $.
      $( [9-Mar-2012] $) $( [4-Sep-2011] $)
  $}


  ${
    $d A x $.  $d X x $.
    noxpsgn.1 $e |- X e. { 1o , 2o } $.
    $( The cross product of an ordinal and the singleton of a sign is a
       surreal. $)
    noxpsgn $p |- ( A e. On -> ( A X. { X } ) e. No ) $=
      ( vx con0 wcel cv c1o c2o cpr csn cxp wf wrex csur wss elisseti fconst
      snss mpbi fss mp2an feq2 rcla4ev mpan2 elno sylibr ) AEFZDGZHIJZABKZLZMZD
      ENZULOFUHAUJULMZUNAUKULMUKUJPZUOABBUJCQZRBUJFUPCBUJUQSTAUKUJULUAUBUMUODAE
      UIAUJULUCUDUEDULUFUG $.
      $( [10-Jul-2011] $) $( [21-Jun-2011] $)
  $}

  $( The cross product of an ordinal and ` { 1o } ` is a surreal. $)
  noxp1o $p |- ( A e. On -> ( A X. { 1o } ) e. No ) $=
    ( c1o c2o con0 1on elisseti prid1 noxpsgn ) ABBCBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

  $( The cross product of an ordinal and ` { 2o } ` is a surreal. $)
  noxp2o $p |- ( A e. On -> ( A X. { 2o } ) e. No ) $=
    ( c2o c1o con0 2on elisseti prid2 noxpsgn ) ABCBBDEFGH $.
    $( [10-Jul-2011] $) $( [12-Jun-2011] $)

