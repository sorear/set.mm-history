$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Tarskian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c EE Btwn $.

  $( Declare the syntax for the Euclidean space generator. $)
  cee $a class EE $.

  $( Declare the syntax for the Euclidean betweenness predicate. $)
  cbtwn $a class Btwn $.

  $( Declare the syntax for the Euclidean congruence predicate. $)
  ccgr $a class == $.

  $( Define the Euclidean space generator.  For details, see ~ elee . $)
  df-ee $a |- EE = ( n e. NN |-> ( RR ^m ( 1 ... n ) ) ) $.

  ${
    $d n x y z t i $.
    $( Define the Euclidean betweenness predicate.  For details, see
       ~ brbtwn . $)
    df-btwn $a |- Btwn =
      `' { <. <. x , z >. , y >. |
         E. n e. NN (
           ( x e. ( EE ` n ) /\ z e. ( EE ` n ) /\ y e. ( EE ` n ) ) /\
           E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... n )
             ( y ` i ) = ( ( ( 1 - t ) x. ( x ` i ) ) + ( t x. ( z ` i ) ) ) )
         } $.
  $}

  ${
    $d n x y i $.
    $( Define the Euclidean congruence predicate.  For details, see
       ~ brcgr . $)
    df-cgr $a |- == =
       { <. x , y >. |
         E. n e. NN
           ( ( x e. ( ( EE ` n ) X. ( EE ` n ) ) /\
             y e. ( ( EE ` n ) X. ( EE ` n ) ) ) /\
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` x ) ` i ) - ( ( 2nd ` x ) ` i ) ) ^ 2 ) =
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` y ) ` i ) - ( ( 2nd ` y ) ` i ) ) ^ 2 ) ) } $.
  $}

  ${
    $d N n $.
    $( Membership in a Euclidean space.  We define Euclidean space here using
       Cartesian coordinates over ` N ` space.  We later abstract away from
       this using Tarski's geometry axioms, so this exact definition is
       unimportant.  (Contributed by Scott Fenton, 3-Jun-2013.) $)
    elee $p |- ( N e. NN -> ( A e. ( EE ` N ) <-> A : ( 1 ... N ) --> RR ) ) $=
      ( vn cn wcel cee cfv cr c1 cfz co cmap wf wceq opreq2 opreq2d df-ee oprex
      cv fvmpt eleq2d reex elmap syl6bb ) BDEZABFGZEAHIBJKZLKZEUGHAMUEUFUHACBHI
      CSZJKZLKUHDFUIBNUJUGHLUIBIJOPCQHUGLRTUAHUGAUBIBJRUCUD $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N a k $.  $d A a $.  $d B a $.  $d F a $.
    $( A condition for a mapping to be an element of a Euclidean space.
       (Contributed by Scott Fenton, 7-Jun-2013.) $)
    mptelee $p |- ( N e. NN ->
      ( ( k e. ( 1 ... N ) |-> ( A F B ) ) e. ( EE ` N ) <->
        A. k e. ( 1 ... N ) ( A F B ) e. RR ) ) $=
      ( va cn wcel co cr wral wss cv a1i wi wal bicomi ra4 sylbi bitri cfz cmpt
      c1 cee cfv elee crn wfn df-f cvv oprex rgen eqid mptfng mpbi mpbiran wceq
      wf wrex cab rnmpt sseq1i abss hbre1 ax-17 hbim hbal r19.23v albii ralcom4
      clel2 biimpri syld ralrimi hbra1 eleq1a rexlimd alrimiv impbii syl6bb
      syl6 ) EGHCUCEUAIZABDIZUBZEUDUEHWBJWDURZWCJHZCWBKZWDEUFWEWDUGZJLZWGWEWDWB
      UHZWIWBJWDUIWCUJHZCWBKWJWKCWBWKCMWBHZABDUKZNULCWBWCWDWDUMZUNUOUPWIFMZWCUQ
      ZCWBUSZFUTZJLZWGWHWRJCFWBWCWDWNVAVBWSWQWOJHZOZFPZWGWQFJVCXBWGXBWFCWBXACFW
      QWTCWPCWBVDWTCVEZVFVGXBWPWTOZCWBKZFPZWLWFOZXFXBXEXAFWPWTCWBVHVIQXFXDFPZCW
      BKZXGXIXFXDCFWBVJQXIWLXHWFXHCWBRXHWFOXIWFXHFWCJWMVKVLNVMSSVNWGXAFWGWPWTCW
      BWFCWBVOXCWGWLWFXDWFCWBRWCJWOVPWAVQVRVSTTTVT $.
      $( [7-Jun-2013] $)
  $}

  ${
    $( If ` A ` is in ` ( EE `` N ) ` , then ` N ` is a natural.  (Contributed
       by Scott Fenton, 1-Jul-2013.) $)
    eleenn $p |- ( A e. ( EE ` N ) -> N e. NN ) $=
      ( vn cee cfv wcel c0 wceq wn cn n0i cdm cr c1 cv cfz co cmap cmpt cvv a1i
      df-ee dmeqi dmmpt oprex mprg eqtri eleq2i ndmfv sylnbir con1i syl ) ABDEZ
      FUMGHZIBJFZUMAKUOUNUOBDLZFUNUPJBUPCJMNCOZPQZRQZSZLZJDUTCUBUCUSTFZVAJHCJCJ
      USTUDVBUQJFMURRUEUAUFUGUHBDUIUJUKUL $.
      $( [1-Jul-2013] $)
  $}

  $( The forward direction of ~ elee .  (Contributed by Scott Fenton,
     1-Jul-2013.) $)
  eleei $p |- ( A e. ( EE ` N ) -> A : ( 1 ... N ) --> RR ) $=
    ( cee cfv wcel c1 cfz co cr wf cn wb eleenn elee syl ibi ) ABCDEZFBGHIAJZQB
    KEQRLABMABNOP $.
    $( [1-Jul-2013] $)

  $( A point belongs to at most one Euclidean space.  (Contributed by Scott
     Fenton, 1-Jul-2013.) $)
  eedimeq $p |- ( ( A e. ( EE ` N ) /\ A e. ( EE ` M ) ) -> N = M ) $=
    ( cee cfv wcel wa c1 wceq cfz co cr wf eleei cdm sylan9req syl2an cn eleenn
    fdm cuz wb nnuz syl6eleq fzopth mpbid simprd ) ACDEFZABDEFZGZHHIZCBIZUJHCJK
    ZHBJKZIZUKULGZUHUMLAMZUNLAMZUOUIACNABNUQURUMAOUNUMLATUNLATPQUHCHUAEZFBRFUOU
    PUBUIUHCRUSACSUCUDABSRHBHCUEQUFUG $.
    $( [1-Jul-2013] $)

  ${
    $d N a n x y z i t $.  $d A x y z i t n $.  $d B x y z i t n $.
    $d C x y z i t n $.
    $( The binary relationship form of the betweenness predicate.  The
       statement ` A Btwn <. B , C >. ` should be informally read as " ` A `
       lies on a line segment between ` B ` and ` C ` .  This exact definition
       is abstracted away by Tarski's geometry axioms later on.  (Contributed
       by Scott Fenton, 3-Jun-2013.) $)
    brbtwn $p |- (
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
      ( A Btwn <. B , C >. <->
        E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... N )
           ( A ` i ) =
           ( ( ( 1 - t ) x. ( B ` i ) ) + ( t x. ( C ` i ) ) ) ) ) $=
      ( vn cv cfv wcel w3a c1 co cmul caddc wceq wral wrex wa cn vy vz vx cbtwn
      cop wbr cee cmin cfz cc0 cicc copab2 ccnv df-btwn breqi wb cvv opex mpan2
      brcnvg 3ad2ant1 df-br eleq1 3anbi1d fveq1 opreq2d opreq1d anbi12d rexbidv
      eqeq2d rexralbidv 3anbi2d 3anbi3d eqeq1d eloprabg wi simp1 eedimeq ancoms
      syl2an opreq2 raleqdv syl biimpd expimpd a1d rexlimdv eleenn fveq2 eleq2d
      3anbi123d rcla4ev exp32 mpcom impbid bitrd 3comr syl5bb ) BCDUEZUDUFBWSUA
      HZGHZUGIZJZUBHZXBJZUCHZXBJZKZEHZXFIZLAHZUHMZXIWTIZNMZXKXIXDIZNMZOMZPZELXA
      UIMZQAUJLUKMZRZSZGTRZUAUBUCULZUMZUFZBFUGIZJZCYGJZDYGJZKZXIBIZXLXICIZNMZXK
      XIDIZNMZOMZPZELFUIMZQZAXTRZBWSUDYEUAUCUBAEGUNUOYKYFWSBYDUFZUUAYHYIYFUUBUP
      ZYJYHWSUQJUUCCDURBWSYGUQYDUTUSVAUUBWSBUEYDJZYKUUAWSBYDVBYIYJYHUUDUUAUPYIY
      JYHKZUUDCXBJZDXBJZBXBJZKZYREXSQZAXTRZSZGTRZUUAYCUUFXEXGKZXJYNXPOMZPZEXSQA
      XTRZSZGTRUUFUUGXGKZXJYQPZEXSQAXTRZSZGTRUUMUAUBUCCDBYGYGYGWTCPZYBUURGTUVCX
      HUUNYAUUQUVCXCUUFXEXGWTCXBVCVDUVCXRUUPAEXTXSUVCXQUUOXJUVCXNYNXPOUVCXMYMXL
      NXIWTCVEVFVGVJVKVHVIXDDPZUURUVBGTUVDUUNUUSUUQUVAUVDXEUUGUUFXGXDDXBVCVLUVD
      UUPUUTAEXTXSUVDUUOYQXJUVDXPYPYNOUVDXOYOXKNXIXDDVEVFVFVJVKVHVIXFBPZUVBUULG
      TUVEUUSUUIUVAUUKUVEXGUUHUUFUUGXFBXBVCVMUVEUUTYRAEXTXSUVEXJYLYQXIXFBVEVNVK
      VHVIVOUUEUUMUUAUUEUULUUAGTUUEUULUUAVPXATJUUEUUIUUKUUAUUEUUISZUUKUUAUVFXAF
      PZUUKUUAUPUUEYIUUFUVGUUIYIYJYHVQUUFUUGUUHVQUUFYIUVGCFXAVRVSVTUVGUUJYTAXTU
      VGYREXSYSXAFLUIWAWBVIZWCWDWEWFWGFTJZUUEUUAUUMVPYIYJUVIYHCFWHVAUVIUUEUUAUU
      MUULUUEUUASGFTUVGUUIUUEUUKUUAUVGUUFYIUUGYJUUHYHUVGXBYGCXAFUGWIZWJUVGXBYGD
      UVJWJUVGXBYGBUVJWJWKUVHVHWLWMWNWOWPWQWRWPWR $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N n i x y $.  $d A n i x y $.  $d B n i x y $.  $d C n i x y $.
    $d D n i x y $.
    $( The binary relationship form of the congruence predicate.  The statement
       ` <. A , B >. == <. C , D >. ` should be read informally as "the ` N `
       dimensional point ` A ` is as far from ` B ` as ` C ` is from ` D ` , or
       "the line segment ` A B ` is congruent to the line segment ` C D ` .
       This particular definition is encapsulated by Tarski's axioms later on.
       (Contributed by Scott Fenton, 3-Jun-2013.) $)
    brcgr $p |- ( ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
                    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. == <. C , D >. <->
        sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) =
        sum_ i e. ( 1 ... N ) ( ( ( C ` i ) - ( D ` i ) ) ^ 2 ) ) ) $=
      ( vn cfv wcel wa co c1st c2nd cmin c2 cexp csu wceq cn fveq1d vx cop ccgr
      vy wbr cv cee cxp c1 cfz wrex opex eleq1 fveq2 opreq12d opreq1d sumeq2sdv
      anbi1d eqeq1d anbi12d rexbidv anbi2d eqeq2d brab opelxpg simplbda adantll
      df-cgr wb ad2ant2l simplrr eedimeq syl11anc adantlr opreq2 sumeq1d op1stg
      eqeq12d syl adantr op2ndg eqeqan12d bitrd biimpd expimpd rexlimdva eleenn
      ad2antrr wi ad2antll opelxpi anim12i biimpar xpeq12d eleq2d rcla4ev exp32
      jca sylan2 mpcom impbid syl5bb ) ABUBZCDUBZUCUEXCGUFZUGHZXFUHZIZXDXGIZJZU
      IXEUJKZEUFZXCLHZHZXLXCMHZHZNKZOPKZEQZXKXLXDLHZHZXLXDMHZHZNKZOPKZEQZRZJZGS
      UKZAFUGHZIZBYJIZJZCYJIZDYJIZJZJZUIFUJKZXLAHZXLBHZNKZOPKZEQZYRXLCHZXLDHZNK
      ZOPKZEQZRZUAUFZXGIZUDUFZXGIZJZXKXLUUJLHZHZXLUUJMHZHZNKZOPKZEQZXKXLUULLHZH
      ZXLUULMHZHZNKZOPKZEQZRZJZGSUKXHUUMJZXSUVHRZJZGSUKYIUAUDXCXDUCABULCDULUUJX
      CRZUVJUVMGSUVNUUNUVKUVIUVLUVNUUKXHUUMUUJXCXGUMURUVNUVAXSUVHUVNXKUUTXREUVN
      UUSXQOPUVNUUPXNUURXPNUVNXLUUOXMUUJXCLUNTUVNXLUUQXOUUJXCMUNTUOUPUQUSUTVAUU
      LXDRZUVMYHGSUVOUVKXJUVLYGUVOUUMXIXHUULXDXGUMVBUVOUVHYFXSUVOXKUVGYEEUVOUVF
      YDOPUVOUVCYAUVEYCNUVOXLUVBXTUULXDLUNTUVOXLUVDYBUULXDMUNTUOUPUQVCUTVAUAUDE
      GVHVDYQYIUUIYQYHUUIGSYQXESIZJZXJYGUUIUVQXJJZYGUUIUVRYGYRXREQZYRYEEQZRZUUI
      UVRXEFRZYGUWAVIYQXJUWBUVPYQXJJDXFIZYOUWBYPXIUWCYMXHYOXIUWCYNYOXICXFIUWCCD
      XFXFYJVEVFVGVJYMYNYOXJVKDFXEVLVMVNUWBXSUVSYFUVTUWBXKYRXREXEFUIUJVOZVPUWBX
      KYRYEEUWDVPVRZVSYQUWAUUIVIUVPXJYMYPUVSUUCUVTUUHYMYRXRUUBEYMXQUUAOPYMXNYSX
      PYTNYKXNYSRYLYKXLXMAABYJVQTVTYMXLXOBABYJYJWATUOUPUQYPYRYEUUGEYPYDUUFOPYPY
      AUUDYCUUENYPXLXTCYNXTCRYOCDYJVQVTTYPXLYBDCDYJYJWATUOUPUQWBZWHWCWDWEWFFSIZ
      YQUUIYIWIYOUWGYMYNDFWGWJUWGYQUUIYIYQUUIJZUWGXCYJYJUHZIZXDUWIIZJZUWAJZYIUW
      HUWLUWAYQUWLUUIYMUWJYPUWKABYJYJWKCDYJYJWKWLVTYQUWAUUIUWFWMWRYHUWMGFSUWBXJ
      UWLYGUWAUWBXHUWJXIUWKUWBXGUWIXCUWBXFYJXFYJXEFUGUNZUWNWNZWOUWBXGUWIXDUWOWO
      UTUWEUTWPWSWQWTXAXB $.
      $( [3-Jun-2013] $)
  $}

  $( The function value of a point is a real.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveere $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. RR ) $=
    ( cee cfv wcel c1 cfz co cr wf eleei ffvelrn sylan ) ACDEFGCHIZJAKBOFBAEJFA
    CLOJBAMN $.
    $( [10-Jun-2013] $)

  $( The function value of a point is a complex.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveecn $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. CC ) $=
    ( cee cfv wcel c1 cfz co wa fveere recnd ) ACDEFBGCHIFJBAEABCKL $.
    $( [10-Jun-2013] $)

  ${
    $d A i $.  $d B i $.  $d N i $.
    $( Two points are equal iff they agree in all dimensions.  (Contributed by
       Scott Fenton, 10-Jun-2013.) $)
    eqeefv $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
       ( A = B <-> A. i e. ( 1 ... N ) ( A ` i ) = ( B ` i ) ) ) $=
      ( cee cfv wcel c1 cfz co wfn wceq cv wral wb cr wf eleei ffn syl eqfnfv2
      syl2an ) ADEFZGZAHDIJZKZBUEKZABLCMZAFUHBFLCUENOBUCGZUDUEPAQUFADRUEPASTUIU
      EPBQUGBDRUEPBSTCUEABUAUB $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    $( Two points are equal iff the square of the distance between them is
       zero.  (Contributed by Scott Fenton, 10-Jun-2013.) $)
    eqeelen $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        ( A = B <->
          sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) = 0 ) ) $=
      ( cfv wcel wa wceq c1 co wral cc0 wb cc fveecn syl syl2an anandirs fveere
      cr cee cv cfz cmin c2 csu subcl sqeq0 subeq0 bitrd ralbidva bicomd eqeefv
      cexp cuz cle wbr cn eleenn syl6eleq adantr resubcl resqcl sqge0 ralrimiva
      nnuz jca fsum00 syl11anc 3bitr4d ) ADUAEZFZBVKFZGZCUBZAEZVOBEZHZCIDUCJZKZ
      VPVQUDJZUEUNJZLHZCVSKZABHVSWBCUFLHZVNWDVTVNWCVRCVSVLVMVOVSFZWCVRMZVLWFGZV
      PNFZVQNFZWGVMWFGZAVODOBVODOWIWJGZWCWALHZVRWLWANFWCWMMVPVQUGWAUHPVPVQUIUJQ
      RUKULABCDUMVNDIUOEZFZWBTFZLWBUPUQZGZCVSKWEWDMVLWOVMVLDURWNADUSVFUTVAVNWRC
      VSVLVMWFWRWHVPTFZVQTFZWRWKAVODSBVODSWSWTGWATFZWRVPVQVBXAWPWQWAVCWAVDVGPQR
      VEWBCIDVHVIVJ $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d M i $.
    $( Useful lemma going forward.  The finite sum of a set of squares of reals
       is non-negative.  (Contributed by Scott Fenton, 11-Jun-2013.) $)
    fsumsq $p |- ( ( N e. ( ZZ>= ` M ) /\ A. i e. ( M ... N ) A e. RR ) ->
       0 <_ sum_ i e. ( M ... N ) ( A ^ 2 ) ) $=
      ( cr wcel cfz co wral cuz cfv c2 cexp cc0 cle wbr wa csu resqcl sqge0 jca
      ralimi fsumcmp0 sylan2 ) AEFZBCDGHZIDCJKFALMHZEFZNUGOPZQZBUFINUFUGBROPUEU
      JBUFUEUHUIASATUAUBUGBCDUCUD $.
      $( [11-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( Alternate characterization of betweenness, with no existential
       quantifiers.  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    brbtwn2 $p |-
        ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( A Btwn <. B , C >. <->
            ( A. i e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 /\
              A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) ) $=
      ( wcel wbr c1 cmin co cmul caddc wceq cc0 cle wa cc syl11anc opreq1d wral
      vk vt vp cfv w3a cv wrex wi cr fveere 3ad2antl2 3ad2antl3 resubcl 3adant3
      cneg recnd syl opreq2d 3ad2ant3 sylancr mul4 syl22anc recn 3ad2ant1 subdi
      1re 3ad2ant2 syl111anc ax-1cn subdir mp3an1 3eqtr3d simp1 remulcl subsub4
      mulid2 3eqtrd mulneg1 eqtrd opreq12d wb subge0 mpbird mpbid eqbrtrd 3expa
      mulge0 sylan an32s ralrimiva fveecn anim12i anandirs anandis subcl ancoms
      mulcom simp2r simp2l simp3 mpan mulcl sub32 3eqtr3rd 3eqtr2d syl2an fveq2
      weq eqeq12d rcla4cva opreq2 breq1d opreqan12d anbi12d wo 3ad2antl1 anbi1d
      ralbidva simpl2 eqeefv adantlr fveq1 orbi12d adantr 3adant1 opreq1 eqeq2d
      bitrd ralbidv rcla4ev wne cdiv breq12d simprr simprl clt necomd syl112anc
      ex cee cop cbtwn cfz cicc brbtwn jca cexp sqval 0re elicc2i simp1bi negcl
      c2 nncan subsub3 negeqd readdcl negsubdi2 3eqtr2rd mulneg2 resqcl simp2bi
      simp2 simp3bi sqge0 le0neg2 3adantl1 simp1l simp1r ralrimivva opreqan12rd
      mulsub2 2ralbidva rexlimdva mulsuble0b simpl1 letri3 pm4.25 breq2d anbi2d
      biimprcd ad2antlr syl5bb biimprd adantrd 0elunit 3jca mul02 addid1 eqtr2d
      ad2antll subid1i syl6eq exp32 syldd necon3abid df-ne rexbii bitri syl6bbr
      wn rexnal rcla4v ad2antrl simpl lesub1 ltlen imp simplrr mpbir2and posdif
      letr divelunit div2sub syl23anc lesub2 eqeltrd jaodan syld rcla42v simp11
      simp12 simp13 simpl23 simpl21 simpl12 simpl22 simpl3 subeq0 divdir adddir
      simpl13 necon3bid npncan2 simpl11 addid2 addsub nnncan2 addsubass 3eqtr4d
      add32 eqtr3d eqtr4d simpr adddi npcan addcl divmul div23 divsubdir 3expia
      divid syl331anc com23 ralrimdv anim12d rexlimdv sylbid pm2.61dne impbid
      syl6 ) AFUUAUEZGZBVVCGZCVVCGZUFZABCUUBUUCHUBUGZAUEZIUCUGZJKZVVHBUEZLKZVVJ
      VVHCUEZLKZMKZNZUBIFUUDKZUAZUCOIUUEKZUHZDUGZBUEZVWBAUEZJKZVWBCUEZVWDJKZLKZ
      OPHZDVVRUAZVWEEUGZCUEZVWKAUEZJKZLKZVWKBUEZVWMJKZVWGLKZNZEVVRUADVVRUAZQZUC
      ABCUBFUUFVVGVWAVXAVVGVVSVXAUCVVTVVGVVJVVTGZQZVWCVVKVWCLKZVVJVWFLKZMKZJKZV
      WFVXFJKZLKZOPHZDVVRUAZVXGVWLVVKVWPLKZVVJVWLLKZMKZJKZLKZVWPVXNJKZVXHLKZNZE
      VVRUADVVRUAZVVSVXAUIVXCVXJDVVRVVGVWBVVRGZVXBVXJVVGVYAQZVWCUJGZVWFUJGZQVXB
      VXJVYBVYCVYDVVEVVDVYAVYCVVFBVWBFUKULZVVFVVDVYAVYDVVECVWBFUKUMZUUGVYCVYDVX
      BVXJVYCVYDVXBUFZVXIVVJVVKUPZLKZVWCVWFJKZUUNUUHKZLKZOPVYGVYLVYIVYJVYJLKZLK
      ZVVJVYJLKZVYHVYJLKZLKZVXIVYGVYKVYMVYILVYGVYJRGZVYKVYMNVYGVYJVYCVYDVYJUJGZ
      VXBVWCVWFUNUOZUQZVYJUUIURUSVYGVVJRGZVYRVYHRGZVYRVYQVYNNVXBVYCWUBVYDVXBVVJ
      VXBVVJUJGZOVVJPHZVVJIPHZOIVVJUUJVGUUKZUULZUQZUTZWUAVYGVVKRGZWUCVYGVVKVXBV
      YCVVKUJGZVYDVXBIUJGZWUDWULVGWUHIVVJUNVAZUTZUQZVVKUUMURWUAVVJVYJVYHVYJVBVC
      VYGVYOVXGVYPVXHLVYGVYOVVJVWCLKZVXEJKZVWCVXDJKZVXEJKZVXGVYGWUBVWCRGZVWFRGZ
      VYOWURNZWUJVYCVYDWVAVXBVWCVDVEZVYDVYCWVBVXBVWFVDVHZVVJVWCVWFVFZVIVYGWUQWU
      SVXEJVYGIVVKJKZVWCLKZIVWCLKZVXDJKZWUQWUSVYGWUKWVAWVHWVJNZWUPWVDIRGZWUKWVA
      WVKVJIVVKVWCVKVLZSVYGWVGVVJVWCLVYGWVLWUBWVGVVJNZVJWUJIVVJUUOZVATVYGWVIVWC
      VXDJVYGWVAWVIVWCNZWVDVWCVQZURTVMTVYGWVAVXDRGZVXERGZWUTVXGNZWVDVYGVXDVYGWU
      LVYCVXDUJGZWUOVYCVYDVXBVNVVKVWCVOSZUQZVYGVXEVYGWUDVYDVXEUJGZVXBVYCWUDVYDW
      UHUTZVYCVYDVXBUVDVVJVWFVOSZUQZVWCVXDVXEVPZVIVRVYGVYPVVKVYJLKZUPZVXFVWFJKZ
      UPZVXHVYGWUKVYRVYPWWJNWUPWUAVVKVYJVSSVYGWWIWWKVYGWWIVXDVVKVWFLKZJKZVXDVWF
      VXEJKZJKZWWKVYGWUKWVAWVBWWIWWNNWUPWVDWVEVVKVWCVWFVFVIVYGWWMWWOVXDJVYGWWMI
      VWFLKZVXEJKZWWOVYGWUBWVBWWMWWRNZWUJWVEWVLWUBWVBWWSVJIVVJVWFVKVLZSVYGWWQVW
      FVXEJVYGWVBWWQVWFNZWVEVWFVQZURTVTUSVYGWVRWVBWVSWWPWWKNWWCWVEWWGVXDVWFVXEU
      UPVIVRUUQVYGVXFRGWVBWWLVXHNVYGVXFVYGWWAWWDVXFUJGWWBWWFVXDVXEUURSUQWVEVXFV
      WFUUSSVRWAUUTVYGVYLVVJVVKLKZVYKLKZUPZOPVYGVYLWXCUPZVYKLKZWXEVYGVYIWXFVYKL
      VYGWUBWUKVYIWXFNWUJWUPVVJVVKUVASTVYGWXCRGVYKRGWXGWXENVYGWXCVYGWUDWULWXCUJ
      GZWWEWUOVVJVVKVOSZUQVYGVYKVYGVYSVYKUJGZVYTVYJUVBURZUQWXCVYKVSSVTVYGOWXDPH
      ZWXEOPHZVYGWXHOWXCPHZWXJOVYKPHZWXLWXIVXBVYCWXNVYDVXBWUDWUEWULOVVKPHZWXNWU
      HVXBWUDWUEWUFWUGUVCWUNVXBWXPWUFVXBWUDWUEWUFWUGUVEVXBWUMWUDWXPWUFWBVGWUHIV
      VJWCVAWDVVJVVKWHVCUTWXKVYGVYSWXOVYTVYJUVFURWXCVYKWHVCVYGWXDUJGZWXLWXMWBVY
      GWXHWXJWXQWXIWXKWXCVYKVOSWXDUVGURWEWFWFWGWIWJWKVXCVXSDEVVRVVRVVGVYAVWKVVR
      GZQZVXBVXSVVGWXSQWVAWVBQZVWPRGZVWLRGZQZQZWUBVXSVXBVVEVVFWXSWYDVVDVVEVVFQZ
      VYAWXRWYDWYEVYAQWXTWYEWXRQWYCVVEVVFVYAWXTVVEVYAQWVAVVFVYAQWVBBVWBFWLCVWBF
      WLWMWNVVEVVFWXRWYCVVEWXRQWYAVVFWXRQWYBBVWKFWLCVWKFWLWMWNWMWOUVHWUIWXTWYCW
      UBVXSWXTWYCWUBUFZWXCVYJVWLVWPJKZLKZLKZWXCVWPVWLJKZVWFVWCJKZLKZLKZVXPVXRWY
      FWYHWYLWXCLWYFWYHWYGVYJLKZWYLWYFVYRWYGRGZWYHWYNNWXTWYCVYRWUBVWCVWFWPVEZWY
      CWXTWYOWUBWYBWYAWYOVWLVWPWPWQVHZVYJWYGWRSWYFWYBWYAWVAWVBWYNWYLNWXTWYAWYBW
      UBWSZWXTWYAWYBWUBWTZWVAWVBWYCWUBUVIZWVAWVBWYCWUBUVJZVWLVWPVWCVWFUVMVCVTUS
      WYFWYIVYOVVKWYGLKZLKZVXPWYFWUBWUKVYRWYOWYIXUCNWXTWYCWUBXAZWUBWXTWUKWYCWVL
      WUBWUKVJIVVJWPXBUTZWYPWYQVVJVVKVYJWYGVBVCWYFVYOVXGXUBVXOLWYFVYOWURWUTVXGW
      YFWUBWVAWVBWVCXUDWYTXUAWVFVIWYFWUQWUSVXEJWYFWVHWVJWUQWUSWYFWUKWVAWVKXUEWY
      TWVMSWYFWVGVVJVWCLWUBWXTWVNWYCWVLWUBWVNVJWVOXBUTZTWYFWVIVWCVXDJWYFWVAWVPW
      YTWVQURTVMTWYFWVAWVRWVSWVTWYTWYFWUKWVAWVRXUEWYTVVKVWCXCSZWYFWUBWVBWVSXUDX
      UAVVJVWFXCSZWWHVIVRWYFXUBVWLVXMJKZVXLJKZVWLVXLJKVXMJKZVXOWYFXUBVVKVWLLKZV
      XLJKZXUJWYFWUKWYBWYAXUBXUMNXUEWYRWYSVVKVWLVWPVFVIWYFXULXUIVXLJWYFXULIVWLL
      KZVXMJKZXUIWYFWUBWYBXULXUONZXUDWYRWVLWUBWYBXUPVJIVVJVWLVKVLSWYFXUNVWLVXMJ
      WYFWYBXUNVWLNWYRVWLVQURTVTTVTWYFWYBVXMRGZVXLRGZXUJXUKNWYRWYFWUBWYBXUQXUDW
      YRVVJVWLXCSZWYFWUKWYAXURXUEWYSVVKVWPXCSZVWLVXMVXLXDVIWYFWYBXURXUQXUKVXONW
      YRXUTXUSVWLVXLVXMVPVIVRWAVTWYFWYMVVJWYJLKZVVKWYKLKZLKZVXRWYFWUBWUKWYJRGZW
      YKRGZWYMXVCNXUDXUEWYCWXTXVDWUBVWPVWLWPVHWXTWYCXVEWUBWVBWVAXVEVWFVWCWPWQVE
      VVJVVKWYJWYKVBVCWYFXVAVXQXVBVXHLWYFXVAVVJVWPLKZVXMJKZVWPVXLJKZVXMJKZVXQWY
      FWUBWYAWYBXVAXVGNXUDWYSWYRVVJVWPVWLVFVIWYFXVHXVFVXMJWYFWVGVWPLKZIVWPLKZVX
      LJKZXVFXVHWYFWUKWYAXVJXVLNZXUEWYSWVLWUKWYAXVMVJIVVKVWPVKVLSWYFWVGVVJVWPLX
      UFTWYFXVKVWPVXLJWYFWYAXVKVWPNWYSVWPVQURTXETWYFWYAXURXUQXVIVXQNWYSXUTXUSVW
      PVXLVXMVPVIXFWYFXVBWWMVXDJKZWWOVXDJKZVXHWYFWUKWVBWVAXVBXVNNXUEXUAWYTVVKVW
      FVWCVFVIWYFWWMWWOVXDJWYFWWMWWRWWOWYFWUBWVBWWSXUDXUAWWTSWYFWWQVWFVXEJWYFWV
      BWXAXUAWXBURTVTTWYFXVOVWFVXDJKVXEJKZVXHWYFWVBWVSWVRXVOXVPNXUAXUHXUGVWFVXE
      VXDXDVIWYFWVBWVRWVSXVPVXHNXUAXUGXUHVWFVXDVXEVPVIVTVRWAVTVMWGXGWJUVKVVSVXA
      VXKVXTQVVSVWJVXKVWTVXTVVSVWIVXJDVVRVVSVYAQZVWDVXFNZVWIVXJWBVVQXVRUBVWBVVR
      UBDXIZVVIVWDVVPVXFVVHVWBAXHXVSVVMVXDVVOVXEMXVSVVLVWCVVKLVVHVWBBXHUSXVSVVN
      VWFVVJLVVHVWBCXHUSWAXJXKZXVRVWHVXIOPXVRVWEVXGVWGVXHLVWDVXFVWCJXLZVWDVXFVW
      FJXLZWAXMURXSVVSVWSVXSDEVVRVVRVVSVYAWXRVWSVXSWBZXVQXVRVWMVXNNZXWCVVSWXRQX
      VTVVQXWDUBVWKVVRUBEXIZVVIVWMVVPVXNVVHVWKAXHXWEVVMVXLVVOVXMMXWEVVLVWPVVKLV
      VHVWKBXHUSXWEVVNVWLVVJLVVHVWKCXHUSWAXJXKXVRXWDQVWOVXPVWRVXRXVRXWDVWEVXGVW
      NVXOLXWAVWMVXNVWLJXLXNXWDXVRVWQVXQVWGVXHLVWMVXNVWPJXLXWBUVLXJXGWOUVNXOUWB
      SUVOVVGVXAVWCVWDPHZVWDVWFPHZQZVWFVWDPHZVWDVWCPHZQZXPZDVVRUAZVWTQZVWAVVGVW
      JXWMVWTVVGVWIXWLDVVRVYBVYCVWDUJGZVYDVWIXWLWBVYEVVDVVEVYAXWOVVFAVWBFUKXQZV
      YFVWCVWDVWFUVPVIXSXRVVGXWNVWAUIZBCVVGBCNZXWNBANZVWAVVGXWRXWNXWSUIVVGXWRQZ
      XWMXWSVWTXWTXWSXWMXWTXWSVWCVWDNZDVVRUAZXWMXWTVVEVVDXWSXXBWBVVDVVEVVFXWRXT
      VVDVVEVVFXWRUVQBADFYASXWTXXAXWLDVVRXWTVYAQZXXAXWFXWJQZXWLXXCVYCXWOXXAXXDW
      BVVGVYAVYCXWRVYEYBVVGVYAXWOXWRXWPYBVWCVWDUVRSXXDXXDXXDXPZXXCXWLXXDUVSXWRX
      XEXWLWBVVGVYAXWRXXDXWHXXDXWKXWRXWJXWGXWFXWRVWCVWFVWDPVWBBCYCZUVTUWAXWRXWF
      XWIXWJXWRVWCVWFVWDPXXFXMXRYDUWCUWDYIXSYIUWEUWFYTVVGXWRXWSVWAVVGXWRXWSQZQZ
      OVVTGVVIIVVLLKZOVVNLKZMKZNZUBVVRUAZVWAUWGXXHXXLUBVVRVVGVVHVVRGZXXGXXLVVGX
      XNQZVVIRGZVVLRGZVVNRGZUFZXXGXXLXXOXXPXXQXXRVVDVVEXXNXXPVVFAVVHFWLZXQVVEVV
      DXXNXXQVVFBVVHFWLZULVVFVVDXXNXXRVVECVVHFWLZUMUWHXXSXXGQXXKVVLVVIXXSXXKVVL
      NZXXGXXQXXRXYCXXPXXQXXRQXXKVVLOMKZVVLXXQXXRXXIVVLXXJOMVVLVQVVNUWIZXNXXQXY
      DVVLNXXRVVLUWJYEVTYFYEXWSVVLVVINXXSXWRVVHBAYCUWLUWKWIWJWKVVSXXMUCOVVTVVJO
      NZVVQXXLUBVVRXYFVVPXXKVVIXYFVVMXXIVVOXXJMXYFVVKIVVLLXYFVVKIOJKIVVJOIJXLIV
      JUWMUWNTVVJOVVNLYGWAYHYJYKVAUWOUWPVVGBCYLZUDUGZBUEZXYHCUEZYLZUDVVRUHZXWQV
      VGXYGXYIXYJNZUDVVRUAZUXBZXYLVVGXYNBCVVEVVFXWRXYNWBVVDBCUDFYAYFUWQXYLXYMUX
      BZUDVVRUHXYOXYKXYPUDVVRXYIXYJUWRUWSXYMUDVVRUXCUWTUXAVVGXYKXWQUDVVRVVGXYHV
      VRGZXYKXWQVVGXYQXYKQZQZXWNXYHAUEZXYIJKZXYJXYIJKZYMKZVVTGZVVIIYUCJKZVVLLKZ
      YUCVVNLKZMKZNZUBVVRUAZQVWAXYSXWMYUDVWTYUJXYSXWMXYIXYTPHZXYTXYJPHZQZXYJXYT
      PHZXYTXYIPHZQZXPZYUDXYQXWMYUQUIVVGXYKXWLYUQDXYHVVRDUDXIZXWHYUMXWKYUPYURXW
      FYUKXWGYULYURVWCXYIVWDXYTPVWBXYHBXHZVWBXYHAXHZYNYURVWDXYTVWFXYJPYUTVWBXYH
      CXHZYNXOYURXWIYUNXWJYUOYURVWFXYJVWDXYTPYVAYUTYNYURVWDXYTVWCXYIPYUTYUSYNXO
      YDUXDUXEXYSYUQYUDXYSYUMYUDYUPXYSYUMQZYUDYUAYUBPHZYVBYULYVCXYSYUKYULYOXYSY
      ULYVCWBZYUMXYSXYTUJGZXYJUJGZXYIUJGZYVDVVGVVDXYQYVEXYRVVDVVEVVFVNXYQXYKUXF
      ZAXYHFUKXGZVVGVVFXYQYVFXYRVVDVVEVVFXAYVHCXYHFUKXGZXYSVVEXYQYVGVVDVVEVVFXY
      RXTVVGXYQXYKYPBXYHFUKSZXYTXYJXYIUXGVIYEWEYVBYUAUJGZOYUAPHZYUBUJGZOYUBYQHZ
      YUDYVCWBXYSYVLYUMXYSYVEYVGYVLYVIYVKXYTXYIUNSYEYVBYVMYUKXYSYUKYULYPXYSYVMY
      UKWBZYUMXYSYVEYVGYVPYVIYVKXYTXYIWCSYEWDXYSYVNYUMXYSYVFYVGYVNYVJYVKXYJXYIU
      NSYEYVBXYIXYJYQHZYVOYVBYVQXYIXYJPHZXYJXYIYLZXYSYVQYVRYVSQWBZYUMXYSYVGYVFY
      VTYVKYVJXYIXYJUXHSYEXYSYUMYVRXYSYVGYVEYVFYUMYVRUIYVKYVIYVJXYIXYTXYJUXMVIU
      XIYVBXYIXYJVVGXYQXYKYUMUXJYRUXKXYSYVQYVOWBZYUMXYSYVGYVFYWAYVKYVJXYIXYJUXL
      SYEWEYUAYUBUXNVCWDXYSYUPQZYUCXYIXYTJKZXYIXYJJKZYMKZVVTXYSYUCYWENZYUPXYSXY
      TRGZXYIRGZXYJRGZYWHYVSYWFXYSXYTYVIUQZXYSXYIYVKUQZXYSXYJYVJUQZYWKXYSXYIXYJ
      VVGXYQXYKYOYRXYTXYIXYJXYIUXOUXPYEYWBYWEVVTGZYWCYWDPHZYWBYUNYWNXYSYUNYUOYP
      XYSYUNYWNWBZYUPXYSYVFYVEYVGYWOYVJYVIYVKXYJXYTXYIUXQVIYEWEYWBYWCUJGZOYWCPH
      ZYWDUJGZOYWDYQHZYWMYWNWBXYSYWPYUPXYSYVGYVEYWPYVKYVIXYIXYTUNSYEYWBYWQYUOXY
      SYUNYUOYOXYSYWQYUOWBZYUPXYSYVGYVEYWTYVKYVIXYIXYTWCSYEWDXYSYWRYUPXYSYVGYVF
      YWRYVKYVJXYIXYJUNSYEYWBXYJXYIYQHZYWSYWBYXAXYJXYIPHZXYKXYSYXAYXBXYKQWBZYUP
      XYSYVFYVGYXCYVJYVKXYJXYIUXHSYEXYSYUPYXBXYSYVFYVEYVGYUPYXBUIYVJYVIYVKXYJXY
      TXYIUXMVIUXIVVGXYQXYKYUPUXJUXKXYSYXAYWSWBZYUPXYSYVFYVGYXDYVJYVKXYJXYIUXLS
      YEWEYWCYWDUXNVCWDUXRUXSYTUXTXYSVWTYUIUBVVRXYSXXNVWTYUIVVGXYRXXNVWTYUIUIVV
      GXYRXXNUFZVWTYWCVVNVVIJKZLKZVVLVVIJKZXYJXYTJKZLKZNZYUIYXEXYQXXNVWTYXKUIVV
      GXYQXYKXXNWTVVGXYRXXNXAZVWSYXKYWCVWNLKZVWQYXILKZNDEXYHVVHVVRVVRYURVWOYXMV
      WRYXNYURVWEYWCVWNLYURVWCXYIVWDXYTJYUSYUTWATYURVWGYXIVWQLYURVWFXYJVWDXYTJY
      VAYUTWAUSXJEUBXIZYXMYXGYXNYXJYXOVWNYXFYWCLYXOVWLVVNVWMVVIJVWKVVHCXHVWKVVH
      AXHZWAUSYXOVWQYXHYXILYXOVWPVVLVWMVVIJVWKVVHBXHYXPWATXJUYASYXEXXPXXQXXRYWG
      YWHYWIYVSYXKYUIUIYXEVVDXXNXXPVVDVVEVVFXYRXXNUYBYXLXXTSYXEVVEXXNXXQVVDVVEV
      VFXYRXXNUYCYXLXYASYXEVVFXXNXXRVVDVVEVVFXYRXXNUYDYXLXYBSVVGXYRYWGXXNYWJUOV
      VGXYRYWHXXNYWKUOVVGXYRYWIXXNYWLUOYXEXYIXYJVVGXYQXYKXXNWSYRXXSYWGYWHYWIUFZ
      YVSUFZYXKYUIYXRYXKQZYXIVVLLKZYUAVVNLKZMKZYUBYMKZYXTYUBYMKZYYAYUBYMKZMKZVV
      IYUHYXSYXTRGZYYARGZYUBRGZYUBOYLZYYCYYFNYXSYXIRGZXXQYYGYXSYWIYWGYYKYWGYWHY
      WIXXSYVSYXKUYEZYWGYWHYWIXXSYVSYXKUYFZXYJXYTWPSZXXPXXQXXRYXQYVSYXKUYGZYXIV
      VLXCSZYXSYUARGZXXRYYHYXSYWGYWHYYQYYMYWGYWHYWIXXSYVSYXKUYHZXYTXYIWPSZXXPXX
      QXXRYXQYVSYXKUYMZYUAVVNXCSZYXSYWIYWHYYIYYLYYRXYJXYIWPSZYXSYYJYVSXXSYXQYVS
      YXKUYIYXSYUBOXYJXYIYXSYWIYWHYUBONXYJXYINWBYYLYYRXYJXYIUYJSUYNWDZYXTYYAYUB
      UYKYSYXSYYCVVINZYUBVVILKZYYBNZYXSUUUEYXGYXIVVILKZMKZYYAMKZYXJUUUGMKZYYAMK
      YYBYXSUUUEYWCVVNLKZUUUEMKZYYAMKZUUUIYXSUUUKYYAMKZUUUEMKZOUUUEMKZUUUMUUUEY
      XSUUUNOUUUEMYXSYWCYUAMKZVVNLKZXXJUUUNOYXSUUUQOVVNLYXSYWHYWGUUUQONYYRYYMXY
      IXYTUYOSTYXSYWCRGZYYQXXRUUURUUUNNYXSYWHYWGUUUSYYRYYMXYIXYTWPSZYYSYYTYWCYU
      AVVNUYLVIYXSXXRXXJONYYTXYEURVMTYXSUUUKRGZYYHUUUERGZUUUOUUUMNYXSUUUSXXRUUV
      AUUUTYYTYWCVVNXCSZUUUAYXSYYIXXPUUVBUUUBXXPXXQXXRYXQYVSYXKUYPZYUBVVIXCSZUU
      UKYYAUUUEVUBVIYXSUUVBUUUPUUUENUUVEUUUEUYQURXEYXSUUULUUUHYYAMYXSUUUKUUUGMK
      YWCVVILKZJKZUUUKUUVFJKZUUUGMKZUUULUUUHYXSUUVAUUUGRGZUUVFRGZUUVGUUVINUUVCY
      XSYYKXXPUUVJYYNUUVDYXIVVIXCSZYXSUUUSXXPUUVKUUUTUUVDYWCVVIXCSZUUUKUUUGUUVF
      UYRVIYXSUUULUUUKUUUGUUVFJKZMKZUUVGYXSUUUEUUVNUUUKMYXSYXIYWCJKZVVILKZUUUEU
      UVNYXSUUVPYUBVVILYXSYWIYWHYWGUUVPYUBNYYLYYRYYMXYJXYIXYTUYSVITYXSYYKUUUSXX
      PUUVQUUVNNYYNUUUTUUVDYXIYWCVVIVKVIVUCUSYXSUUVAUUVJUUVKUUVGUUVONUUVCUUVLUU
      VMUUUKUUUGUUVFUYTVIVUDYXSYXGUUVHUUUGMYXSUUUSXXRXXPYXGUUVHNUUUTYYTUUVDYWCV
      VNVVIVFVITVUATVTYXSUUUHUUUJYYAMYXSYXGYXJUUUGMYXRYXKVUETTYXSUUUJYXTYYAMYXS
      UUUJYXIYXHLKZUUUGMKZYXIYXHVVIMKZLKZYXTYXSYXJUUVRUUUGMYXSYXHRGZYYKYXJUUVRN
      YXSXXQXXPUUWBYYOUUVDVVLVVIWPSZYYNYXHYXIWRSTYXSYYKUUWBXXPUUWAUUVSNYYNUUWCU
      UVDYXIYXHVVIVUFVIYXSUUVTVVLYXILYXSXXQXXPUUVTVVLNYYOUUVDVVLVVIVUGSUSXFTVRY
      XSYYBRGZXXPYYIYYJUUUDUUUFWBYXSYYGYYHUUWDYYPUUUAYXTYYAVUHSUUVDUUUBUUUCYYBV
      VIYUBVUIYSWDYXSYYDYUFYYEYUGMYXSYYDYXIYUBYMKZVVLLKZYUFYXSYYKXXQYYIYYJYYDUU
      WFNYYNYYOUUUBUUUCYXIVVLYUBVUJYSYXSUUWEYUEVVLLYXSYUBYUAJKZYUBYMKZYUBYUBYMK
      ZYUCJKZUUWEYUEYXSYYIYYQYYIYYJUUWHUUWJNUUUBYYSUUUBUUUCYUBYUAYUBVUKYSYXSUUW
      GYXIYUBYMYXSYWIYWGYWHUUWGYXINYYLYYMYYRXYJXYTXYIUYSVITYXSUUWIIYUCJYXSYYIYY
      JUUWIINUUUBUUUCYUBVUMSTVMTVTYXSYYQXXRYYIYYJYYEYUGNYYSYYTUUUBUUUCYUAVVNYUB
      VUJYSWAVMYTVUNUXTVULVUOVUPVUQVVSYUJUCYUCVVTVVJYUCNZVVQYUIUBVVRUUWKVVPYUHV
      VIUUWKVVMYUFVVOYUGMUUWKVVKYUEVVLLVVJYUCIJXLTVVJYUCVVNLYGWAYHYJYKVVBUWOVUR
      VUSVUTVUSVVAYI $.
      $( [24-Jun-2013] $)
  $}


  $( Lemma for ~ colinearalg .  Expand out a multiplication. $)
  colinearalglem1 $p |- (
    ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
      ( D e. CC /\ E e. CC /\ F e. CC ) ) ->
    ( ( ( B - A ) x. ( F - D ) ) = ( ( E - D ) x. ( C - A ) ) <->
      ( ( B x. F ) - ( ( A x. F ) + ( B x. D ) ) ) =
      ( ( C x. E ) - ( ( A x. E ) + ( C x. D ) ) ) ) ) $=
    ( cc wcel w3a cmin co cmul wceq caddc subcl syl11anc syl111anc mulcl syl2an
    subdir simpl2 simpl1 simpr3 simpr1 subdi simp2 simp3 simp1 opreq12d subsub3
    wa addsub eqtr2d subsub4 opreq1d 3eqtr3d eqtrd simpr2 simpl3 mulcom eqeq12d
    3eqtrd wb addcl addcan2 bitrd ) AGHZBGHZCGHZIZDGHZEGHZFGHZIZUKZBAJKZFDJKLKZ
    EDJKZCAJKZLKZMBFLKZAFLKZBDLKZNKZJKZADLKZNKZCELKZAELKZCDLKZNKZJKZWFNKZMZWEWL
    MZVOVQWGVTWMVOVQVPFLKZVPDLKZJKZWGVOVPGHZVMVKVQWRMVOVHVGWSVGVHVIVNUAZVGVHVIV
    NUBZBAOPVJVKVLVMUCZVJVKVLVMUDZVPFDUEQVOWAWBJKZWFNKWCJKZXDWCJKZWFNKZWRWGVOXD
    GHZWFGHZWCGHZXEXGMVOWAGHZWBGHZXHVJVHVMXKVNVGVHVIUFZVKVLVMUGZBFRSZVJVGVMXLVN
    VGVHVIUHZXNAFRSZWAWBOPZVJVGVKXIVNXPVKVLVMUHZADRSZVJVHVKXJVNXMXSBDRSZXDWFWCU
    LQVOWRXDWCWFJKZJKZXEVOWPXDWQYBJVOVHVGVMWPXDMWTXAXBBAFTQVOVHVGVKWQYBMWTXAXCB
    ADTQUIVOXHXJXIYCXEMXRYAXTXDWCWFUJQUMVOXFWEWFNVOXKXLXJXFWEMXOXQYAWAWBWCUNQUO
    UPUQVOVTVSVRLKZVSELKZVSDLKZJKZWMVOVRGHZVSGHZVTYDMVOVLVKYHVJVKVLVMURZXCEDOPV
    OVIVGYIVGVHVIVNUSZXACAOPZVRVSUTPVOYIVLVKYDYGMYLYJXCVSEDUEQVOWHWIJKZWFNKWJJK
    ZYMWJJKZWFNKZYGWMVOYMGHZXIWJGHZYNYPMVOWHGHZWIGHZYQVJVIVLYSVNVGVHVIUGZVKVLVM
    UFZCERSZVJVGVLYTVNXPUUBAERSZWHWIOPZXTVJVIVKYRVNUUAXSCDRSZYMWFWJULQVOYGYMWJW
    FJKZJKZYNVOYEYMYFUUGJVOVIVGVLYEYMMYKXAYJCAETQVOVIVGVKYFUUGMYKXAXCCADTQUIVOY
    QYRXIUUHYNMUUEUUFXTYMWJWFUJQUMVOYOWLWFNVOYSYTYRYOWLMUUCUUDUUFWHWIWJUNQUOUPV
    BVAVOWEGHZWLGHZXIWNWOVCVOXKWDGHZUUIXOVOXLXJUUKXQYAWBWCVDPWAWDOPVOYSWKGHZUUJ
    UUCVOYTYRUULUUDUUFWIWJVDPWHWKOPXTWEWLWFVEQVF $.
    $( [24-Jun-2013] $)

  ${
    $d A i j $.  $d B i j $.  $d C i j $.  $d N i j $.
    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem2 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( C ` i ) - ( B ` i ) ) x. ( ( A ` j ) - ( B ` j ) ) ) =
         ( ( ( C ` j ) - ( B ` j ) ) x. ( ( A ` i ) - ( B ` i ) ) ) ) ) $=
      ( cfv wcel w3a cmin co cmul wceq cc fveecn caddc mulcl syl11anc syl111anc
      syl2an cee cv c1 wa wb simp1 simpl simp2 simp3 simpr addcl subsub4 eqcomd
      subsub3 opreq2d subcl subsub eqtrd 3eqtr2d addsub12 3eqtrd eqeq1d subadd2
      eqcom syl6bb addsub addcom opreq1d eqtr3d eqeq2d subeqrev syl22anc 3bitrd
      cfz subadd syl5rbbr sub32 3bitr3d colinearalglem1 3anrot syl2anb syl33anc
      bitrd 3bitr4d 2ralbidva ) AFUAGZHZBWFHZCWFHZIZDUBZBGZWKAGZJKEUBZCGZWNAGZJ
      KLKWNBGZWPJKWKCGZWMJKLKMZWRWLJKWPWQJKLKWOWQJKWMWLJKLKMZDEUCFVNKZXAWJWKXAH
      ZWNXAHZUDZUDWMNHZWLNHZWRNHZWPNHZWQNHZWONHZWSWTUEWJWGXBXEXDWGWHWIUFZXBXCUG
      ZAWKFOTWJWHXBXFXDWGWHWIUHZXLBWKFOTWJWIXBXGXDWGWHWIUIZXLCWKFOTWJWGXCXHXDXK
      XBXCUJZAWNFOTWJWHXCXIXDXMXOBWNFOTWJWIXCXJXDXNXOCWNFOTXEXFXGIZXHXIXJIZUDZW
      LWOLKZWMWOLKZWLWPLKZPKZJKWRWQLKZWMWQLKZWRWPLKZPKZJKMZYEYAYCPKJKZXTXSYDPKZ
      JKZMZWSWTXRYBXSJKZYDJKZYEYCJKZMZYAYJPKZYNMZYGYKXRYMYPYNXRYMXTXSJKZYAYDJKZ
      PKZYAYRYDJKZPKZYPXRYMYBYIJKZXTYIYAJKZJKZYTXRYBNHZXSNHZYDNHZYMUUCMXRXTNHZY
      ANHZUUFXPXEXJUUIXQXEXFXGUFZXHXIXJUIZWMWOQTZXPXFXHUUJXQXEXFXGUHZXHXIXJUFZW
      LWPQTZXTYAUKRZXPXFXJUUGXQUUNUULWLWOQTZXPXEXIUUHXQUUKXHXIXJUHZWMWQQTZYBXSY
      DULSXRUUIYINHZUUJUUEUUCMUUMXRUUGUUHUVAUURUUTXSYDUKRZUUPXTYIYAUNSXRUUEXTXS
      YSJKZJKZYTXRUUDUVCXTJXRUVCUUDXRUUGUUJUUHUVCUUDMUURUUPUUTXSYAYDUNSUMUOXRUU
      IUUGYSNHZUVDYTMUUMUURXRUUJUUHUVEUUPUUTYAYDUPRXTXSYSUQSURUSXRYRNHZUUJUUHYT
      UUBMXRUUIUUGUVFUUMUURXTXSUPRUUPUUTYRYAYDUTSXRUUAYJYAPXRUUIUUGUUHUUAYJMUUM
      UURUUTXTXSYDULSUOVAVBXRYOYLYNYDPKZMZYLYFYCJKZMZYGXRYOUVGYLMZUVHXRYLNHZUUH
      YNNHZYOUVKUEXRUUFUUGUVLUUQUURYBXSUPRUUTXRYENHZYCNHZUVMXPXGXHUVNXQXEXFXGUI
      ZUUOWRWPQTZXPXGXIUVOXQUVPUUSWRWQQTZYEYCUPRZYLYDYNVCSUVGYLVDVEXRUVGUVIYLXR
      YEYDPKZYCJKZUVGUVIXRUVNUUHUVOUWAUVGMUVQUUTUVRYEYDYCVFSXRUVTYFYCJXRUVNUUHU
      VTYFMUVQUUTYEYDVGRVHVIVJXRUUFUUGYFNHZUVOUVJYGUEUUQUURXRUUHUVNUWBUUTUVQYDY
      EUKRUVRYBXSYFYCVKVLVMXRYQYJYHMZYKXRYQYJYNYAJKZMZUWCUWEUWDYJMZXRYQUWDYJVDX
      RUVMUUJYJNHZUWFYQUEUVSUUPXRUUIUVAUWGUUMUVBXTYIUPRYNYAYJVOSVPXRUWDYHYJXRUW
      DYEYAJKYCJKZYHXRUVNUVOUUJUWDUWHMUVQUVRUUPYEYCYAVQSXRUVNUUJUVOUWHYHMUVQUUP
      UVRYEYAYCULSURVJWCYJYHVDVEVRWMWLWRWPWQWOVSXPXFXGXEIXIXJXHIWTYKUEXQXEXFXGV
      TXHXIXJVTWLWRWMWQWOWPVSWAWDWBWE $.
      $( [24-Jun-2013] $)

    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem3 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( A ` i ) - ( C ` i ) ) x. ( ( B ` j ) - ( C ` j ) ) ) =
         ( ( ( A ` j ) - ( C ` j ) ) x. ( ( B ` i ) - ( C ` i ) ) ) ) ) $=
      ( cee cfv wcel w3a cv cmin co cmul wceq c1 cfz wral colinearalglem2 wb
      3comr bitrd ) AFGHZIZBUCIZCUCIZJDKZBHZUGAHZLMEKZCHZUJAHZLMNMUJBHZULLMUGCH
      ZUILMNMOEPFQMZRDUORUNUHLMULUMLMNMUKUMLMUIUHLMNMOEUORDUORZUIUNLMUMUKLMNMUL
      UKLMUHUNLMNMOEUORDUORZABCDEFSUEUFUDUPUQTBCADEFSUAUB $.
      $( [24-Jun-2013] $)


  $}

  ${
    $d A i $.  $d C i $.  $d K i $.  $d N i $.
    $( Lemma for ~ colinearalg .  Prove a disjunction that will be needed in
       the final proof. $)
    colinearalglem4 $p |- (
       ( ( A e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\ K e. RR ) ->
       ( A. i e. ( 1 ... N ) ( ( ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) +
         ( A ` i ) ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( C ` i ) - ( ( K x. ( ( C ` i ) -
         ( A ` i ) ) ) + ( A ` i ) ) ) x. ( ( A ` i ) -
         ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( A ` i ) - ( C ` i ) ) x. ( ( ( K x.
         ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) - ( C ` i ) ) ) <_ 0 ) ) $=
      ( wcel wa cr cc0 cle wbr c1 cmin co cmul cc wceq recnd syl11anc syl caddc
      cee cfv w3o cv cfz wral relin01 adantl fveere adantlr adantll jca c2 cexp
      simprl resubcl ancoms adantr mulass syl111anc remulcl recn ad2antrr pncan
      opreq1d sqval opreq2d 3eqtr4d wo simprr sqge0 orcd resqcl mulle0b eqbrtrd
      wb mpbird sylan an32s ralrimiva expr ad2antlr ax-1cn subdir mp3an1 mulid2
      cneg sub32 eqtr2d subsub4 3eqtr3rd subid df-neg syl6eqr opreq12d 1re mpan
      ad2antrl mulneg2 mul4 syl22anc negeqd 3eqtrd simpl sylancr subge0 biimpar
      adantrl mulge0 breqtrd le0neg2 mpbid negsubdi2 simplr simpll mul12 eqtr4d
      peano2rem mulneg1 eqtrd mp3an2 subsub3 mpan2 3orim123d mpd ) AEUBUCZFZBYG
      FZGZDHFZGZDIJKZIDJKZDLJKZGZLDJKZUDZDCUEZBUCZYSAUCZMNZONZUUAUANZUUAMNZUUBO
      NZIJKZCLEUFNZUGZYTUUDMNZUUAUUDMNZONZIJKZCUUHUGZUUAYTMNZUUDYTMNZONZIJKZCUU
      HUGZUDYKYRYJDUHUIYLYMUUIYPUUNYQUUSYJYKYMUUIYJYKYMGZGUUGCUUHYJYSUUHFZUUTUU
      GYJUVAGZUUAHFZYTHFZGZUUTUUGUVBUVCUVDYHUVAUVCYIAYSEUJUKYIUVAUVDYHBYSEUJULU
      MZUVEUUTGZUUFDUUBUNUONZONZIJUVGUUCUUBONZDUUBUUBONZONZUUFUVIUVGDPFZUUBPFZU
      VNUVJUVLQUVGDUVEYKYMUPZRUVGUUBUVEUUBHFZUUTUVDUVCUVPYTUUAUQZURZUSZRZUVTDUU
      BUUBUTVAUVGUUEUUCUUBOUVGUUCPFZUUAPFZUUEUUCQUVGUUCUVGYKUVPUUCHFZUVOUVSDUUB
      VBZSRUVCUWBUVDUUTUUAVCZVDUUCUUAVESVFUVGUVHUVKDOUVGUVNUVHUVKQZUVTUUBVGZTVH
      VIUVGUVIIJKZYMIUVHJKZGZYNUVHIJKGZVJZUVGUWJUWKUVGYMUWIUVEYKYMVKUVGUVPUWIUV
      SUUBVLZTUMVMUVGYKUVHHFZUWHUWLVQUVOUVGUVPUWNUVSUUBVNZTDUVHVOSVRVPVSVTWAWBY
      JYKYPUUNYJYKYPGZGUUMCUUHYJUVAUWPUUMUVBUVEUWPUUMUVFUVEUWPGZUULLDMNZDONZUVK
      ONZWHZIJUWQUULUWRUUBONZUUCWHZONZUXBUUCONZWHZUXAUWQUUJUXBUUKUXCOUWQUUBUUCM
      NZYTUUCMNUUAMNZUXBUUJUWQYTPFZUWBUWAUXGUXHQUVDUXIUVCUWPYTVCZWCZUVCUWBUVDUW
      PUWEVDZUWQUUCUWQYKUVPUWCUVEYKYPUPZUVEUVPUWPUVRUSZUWDSRZYTUUAUUCWIVAUWQUXB
      LUUBONZUUCMNZUXGUWQUVMUVNUXBUXQQZUWQDUXMRZUWQUUBUXNRZLPFZUVMUVNUXRWDLDUUB
      WEWFSUWQUXPUUBUUCMUWQUVNUXPUUBQZUXTUUBWGZTVFWJUWQUXIUWAUWBUXHUUJQUXKUXOUX
      LYTUUCUUAWKVAWLUWQUUAUUAMNZUUCMNZUUAUUCMNUUAMNZUXCUUKUWQUWBUWBUWAUYEUYFQU
      XLUXLUXOUUAUUAUUCWIVAUWQUYEIUUCMNUXCUWQUYDIUUCMUWQUWBUYDIQUXLUUAWMTVFUUCW
      NWOUWQUWBUWAUWBUYFUUKQUXLUXOUXLUUAUUCUUAWKVAWLWPUWQUXBPFUWAUXDUXFQUWQUXBU
      WQUWRHFZUVPUXBHFYKUYGUVEYPLHFZYKUYGWQLDUQZWRWSZUXNUWRUUBVBSRUXOUXBUUCWTSU
      WQUXEUWTUWQUWRPFUVNUVMUVNUXEUWTQUWQUWRUYJRUXTUXSUXTUWRUUBDUUBXAXBXCXDUWQI
      UWTJKZUXAIJKZUWQIUWSUVHONZUWTJUWQUWSHFZIUWSJKZUWNUWIIUYMJKUWQUYGYKUYNUYJU
      XMUWRDVBSZUWPUYOUVEUWPUYGIUWRJKZYKYNUYOUWPUYHYKUYGWQYKYPXEZUYIXFYKYOUYQYN
      YKUYQYOUYHYKUYQYOVQWQLDXGWRXHXIUYRYKYNYOUPUWRDXJXBUIUWQUVPUWNUXNUWOTUWQUV
      PUWIUXNUWMTUWSUVHXJXBUWQUVHUVKUWSOUWQUVNUWFUXTUWGTVHXKUWQUWTHFZUYKUYLVQUW
      QUYNUVKHFZUYSUYPUWQUVPUVPUYTUXNUXNUUBUUBVBSUWSUVKVBSUWTXLTXMVPVSVTWAWBYJY
      KYQUUSYJYKYQGZGUURCUUHYJUVAVUAUURUVBUVEVUAUURUVFUVEVUAGZUUQDLMNZUVHONZWHZ
      IJVUBUUBWHZVUCUUBONZONZUUOVUGONVUEUUQVUBVUFUUOVUGOVUBUXIUWBVUFUUOQUVDUXIU
      VCVUAUXJWCZUVCUWBUVDVUAUWEVDZYTUUAXNSVFVUBVUHUUBVUGONZWHZVUEVUBUVNVUGPFVU
      HVULQVUBUUBVUBUVDUVCUVPUVCUVDVUAXOUVCUVDVUAXPUVQSZRZVUBVUGVUBVUCHFZUVPVUG
      HFYKVUOUVEYQDXSWSZVUMVUCUUBVBSRUUBVUGXTSVUBVUKVUDVUBVUKVUCUVKONZVUDVUBUVN
      VUCPFUVNVUKVUQQVUNVUBVUCVUPRVUNUUBVUCUUBXQVAVUBUVHUVKVUCOVUBUVNUWFVUNUWGT
      VHXRXCYAVUBVUGUUPUUOOVUBVUGUUCUXPMNZUUCUUBMNZUUPVUBUVMUVNVUGVURQZVUBDUVEY
      KYQUPZRVUNUVMUYAUVNVUTWDDLUUBWEYBSVUBUXPUUBUUCMVUBUVNUYBVUNUYCTVHVUBUWAUX
      IUWBVUSUUPQVUBUUCVUBYKUVPUWCVVAVUMUWDSRVUIVUJUUCYTUUAYCVAXDVHWLVUBIVUDJKZ
      VUEIJKZVUBVUOIVUCJKZUWNUWIVVBVUPVUBVVDYQUVEYKYQVKYKVVDYQVQZUVEYQYKUYHVVEW
      QDLXGYDWSVRVUBUVPUWNVUMUWOTZVUBUVPUWIVUMUWMTVUCUVHXJXBVUBVUDHFZVVBVVCVQVU
      BVUOUWNVVGVUPVVFVUCUVHVBSVUDXLTXMVPVSVTWAWBYEYF $.
      $( [27-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( An algebraic characterization of colinearity.  Note the similarity to
       ~ brbtwn2 .  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    colinearalg $p |-
        (
          ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( ( A Btwn <. B , C >. \/ B Btwn <. C , A >. \/
              C Btwn <. A , B >. ) <->
            A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) $=
      ( cfv wcel wbr cmin co cmul cc0 cle wral wceq wa wb cc syl11anc cee cbtwn
      vp w3a cop w3o cv c1 brbtwn2 3comr colinearalglem3 anbi2d colinearalglem2
      cfz bitrd 3coml 3orbi123d wi fveecn subid opreq2d adantl subcl syl syl2an
      mul01 eqtrd anandirs 0re leidi syl6eqbr ralrimiva 3adant1 opreq12d breq1d
      fveq1 ralbidv syl5ibcom 3mix1 syl6 a1dd wne simp3 simp1 eqeefv necon3abid
      wrex wn df-ne rexbii rexnal bitr2i syl6bb ralcom weq fveq2 opreq1d rcla4v
      eqeq12d ad2antrl cr fveere 3ad2antl1 3ad2antl2 3ad2antl3 3jca anim1i cdiv
      anasss caddc adantlr recn 3anim123i adantr ad2antlr simplrr simp12 simp11
      eqcom simp22 simp21 simp23 simpr3 simpr1 subeq0 necon3bid divcl syl111anc
      biimp3ar mulcl syl112anc syl5bb resubcl opreq1 ralimi ralbi opreq2 df-3or
      wo andir simp13 subadd2 bicomd div23 eqeq2d divmul mulcom eqeq1d ralbidva
      3bitr2d 3simpb simpl2 simpl1 simpl3 recnd biimpar redivcl colinearalglem4
      3ad2ant1 syl5ibrcom sylbird syldan syl5bi exp32 rexlimdv sylbid pm2.61dne
      syld pm4.71rd orbi1i anbi1i bitri 3bitr4i syl6rbb ) AFUAGZHZBUVOHZCUVOHZU
      DZABCUEUBIZBCAUEUBIZCABUEUBIZUFDUGZBGZUWCAGZJKZUWCCGZUWEJKZLKZMNIZDUHFUNK
      ZOZUWFEUGZCGZUWMAGZJKZLKZUWMBGZUWOJKZUWHLKZPZEUWKODUWKOZQZUWGUWDJKZUWEUWD
      JKZLKZMNIZDUWKOZUXBQZUWEUWGJKZUWDUWGJKZLKZMNIZDUWKOZUXBQZUFZUXBUVSUVTUXCU
      WAUXIUWBUXOABCDEFUIUVSUWAUXHUXDUWOUWRJKLKUWNUWRJKUXELKPEUWKODUWKOZQZUXIUV
      QUVRUVPUWAUXRRBCADEFUIUJUVSUXQUXBUXHUVQUVRUVPUXQUXBRBCADEFUKUJULUOUVRUVPU
      VQUWBUXORUVRUVPUVQUDZUWBUXNUXJUWRUWNJKLKUWOUWNJKUXKLKPEUWKODUWKOZQUXOCABD
      EFUIUXSUXTUXBUXNCABDEFUMULUOUPUQUVSUXBUWLUXHUXNUFZUXBQZUXPUVSUXBUYAUVSUXB
      UYAURZCAUVSCAPZUYAUXBUVSUYDUWLUYAUVSUXKUWGUWGJKZLKZMNIZDUWKOZUYDUWLUVQUVR
      UYHUVPUVQUVRQZUYGDUWKUYIUWCUWKHZQUYFMMNUVQUVRUYJUYFMPZUVQUYJQUWDSHZUWGSHZ
      UYKUVRUYJQBUWCFUSZCUWCFUSZUYLUYMQZUYFUXKMLKZMUYMUYFUYQPUYLUYMUYEMUXKLUWGU
      TVAVBUYPUXKSHUYQMPUWDUWGVCUXKVFVDVGVEVHMVIVJVKVLVMUYDUYGUWJDUWKUYDUYFUWIM
      NUYDUXKUWFUYEUWHLUYDUWGUWEUWDJUWCCAVPZVAUYDUWGUWEUWGJUYRVAVNVOVQVRUWLUXHU
      XNVSVTWAUVSCAWBZUCUGZCGZUYTAGZWBZUCUWKWGZUYCUVSUYSVUAVUBPZUCUWKOZWHZVUDUV
      SVUFCAUVSUVRUVPUYDVUFRUVPUVQUVRWCUVPUVQUVRWDCAUCFWETWFVUDVUEWHZUCUWKWGVUG
      VUCVUHUCUWKVUAVUBWIWJVUEUCUWKWKWLWMUVSVUCUYCUCUWKUVSUYTUWKHZVUCUYCUXBUXAD
      UWKOZEUWKOZUVSVUIVUCQZQZUYAUXADEUWKUWKWNVUMVUKUWFVUAVUBJKZLKZUYTBGZVUBJKZ
      UWHLKZPZDUWKOZUYAVUIVUKVUTURUVSVUCVUJVUTEUYTUWKEUCWOZUXAVUSDUWKVVAUWQVUOU
      WTVURVVAUWPVUNUWFLVVAUWNVUAUWOVUBJUWMUYTCWPUWMUYTAWPZVNVAVVAUWSVUQUWHLVVA
      UWRVUPUWOVUBJUWMUYTBWPVVBVNWQWSVQWRWTUVSVULVUBXAHZVUPXAHZVUAXAHZUDZVUCQZV
      UTUYAURUVSVUIVUCVVGUVSVUIQZVVFVUCVVHVVCVVDVVEUVPUVQVUIVVCUVRAUYTFXBXCUVQU
      VPVUIVVDUVRBUYTFXBXDUVRUVPVUIVVEUVQCUYTFXBXEXFXGXIUVSVVGQZVUTUWDVUQVUNXHK
      ZUWHLKZUWEXJKZPZDUWKOZUYAVVIVVMVUSDUWKVVIUYJQUWESHZUYLUYMUDZVUBSHZVUPSHZV
      UASHZUDZVUCVVMVUSRUVSUYJVVPVVGUVSUYJQVVOUYLUYMUVPUVQUYJVVOUVRAUWCFUSXCUVQ
      UVPUYJUYLUVRUYNXDUVRUVPUYJUYMUVQUYOXEXFXKVVGVVTUVSUYJVVFVVTVUCVVCVVQVVDVV
      RVVEVVSVUBXLZVUPXLVUAXLXMXNXOUVSVVFVUCUYJXPVVMVVLUWDPZVVPVVTVUCUDZVUSUWDV
      VLXSVWCVWBUWFVVKPZUWFVURVUNXHKZPZVUSVWCUYLVVOVVKSHZVWBVWDRVVOUYLUYMVVTVUC
      XQZVVOUYLUYMVVTVUCXRZVWCVVJSHZUWHSHZVWGVWCVUQSHZVUNSHZVUNMWBZVWJVWCVVRVVQ
      VWLVVPVVQVVRVVSVUCXTVVPVVQVVRVVSVUCYAZVUPVUBVCTZVWCVVSVVQVWMVVPVVQVVRVVSV
      UCYBVWOVUAVUBVCTZVVPVVTVWNVUCVVPVVTQZVUNMVUAVUBVWRVVSVVQVUNMPVUERZVVPVVQV
      VRVVSYCVVPVVQVVRVVSYDVUAVUBYEZTYFYIZVUQVUNYGYHVWCUYMVVOVWKVVOUYLUYMVVTVUC
      UUAVWIUWGUWEVCTZVVJUWHYJTUYLVVOVWGUDVWDVWBUWDUWEVVKUUBUUCYHVWCVWEVVKUWFVW
      CVWLVWKVWMVWNVWEVVKPVWPVXBVWQVXAVUQUWHVUNUUDYKUUEVWFVWEUWFPZVWCVUSUWFVWEX
      SVWCVXCVUNUWFLKZVURPZVUSVWCVURSHZUWFSHZVWMVWNVXCVXERVWCVWLVWKVXFVWPVXBVUQ
      UWHYJTVWCUYLVVOVXGVWHVWIUWDUWEVCTZVWQVXAVURUWFVUNUUFYKVWCVXDVUOVURVWCVWMV
      XGVXDVUOPVWQVXHVUNUWFUUGTUUHUOYLUUJYLYHUUIUVSUVPUVRQZVVJXAHZVVNUYAURVVGUV
      PUVQUVRUUKVVGVUQXAHZVUNXAHZVWNVXJVVGVVDVVCVXKVVCVVDVVEVUCUULVVCVVDVVEVUCU
      UMZVUPVUBYMTVVGVVEVVCVXLVVCVVDVVEVUCUUNVXMVUAVUBYMTVVFVWNVUCVVFVUNMVUAVUB
      VVFVVSVVQVWSVVFVUAVVCVVDVVEWCUUOVVCVVDVVQVVEVWAUUSVWTTYFUUPVUQVUNUUQYHVXI
      VXJQUYAVVNVVLUWEJKZUWHLKZMNIZDUWKOZUWGVVLJKZUWEVVLJKZLKZMNIZDUWKOZUXJVVLU
      WGJKZLKZMNIZDUWKOZUFACDVVJFUURVVNUWLVXQUXHVYBUXNVYFVVNUWJVXPRZDUWKOUWLVXQ
      RVVMVYGDUWKVVMUWIVXOMNVVMUWFVXNUWHLUWDVVLUWEJYNWQVOYOUWJVXPDUWKYPVDVVNUXG
      VYARZDUWKOUXHVYBRVVMVYHDUWKVVMUXFVXTMNVVMUXDVXRUXEVXSLUWDVVLUWGJYQUWDVVLU
      WEJYQVNVOYOUXGVYADUWKYPVDVVNUXMVYERZDUWKOUXNVYFRVVMVYIDUWKVVMUXLVYDMNVVMU
      XKVYCUXJLUWDVVLUWGJYNVAVOYOUXMVYEDUWKYPVDUQUUTVEUVAUVBUVHUVCUVDUVEUVFUVGU
      VIUWLUXHYSZUXBQZUXOYSZUXCUXIYSZUXOYSUYBUXPVYKVYMUXOUWLUXHUXBYTUVJUYBVYJUX
      NYSZUXBQVYLUYAVYNUXBUWLUXHUXNYRUVKVYJUXNUXBYTUVLUXCUXIUXOYRUVMUVNUO $.
      $( [24-Jun-2013] $)
  $}


  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesub.1 $e |- C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  (Contributed
       by Scott Fenton, 17-Jul-2013.) $)
    eleesub $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel wa c1 cfz co cv cmin cmpt cr wral fveere resubcl ralrimiva
      syl2an anandirs wb cn eleenn mptelee syl adantr mpbird syl5eqel ) AEGHZIZ
      BULIZJZCDKELMZDNZAHZUQBHZOMZPZULFUOVAULIZUTQIZDUPRZUOVCDUPUMUNUQUPIZVCUMV
      EJURQIUSQIVCUNVEJAUQESBUQESURUSTUBUCUAUMVBVDUDZUNUMEUEIVFAEUFURUSDOEUGUHU
      IUJUK $.
      $( [17-Jul-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesubd.1 $e |- ( ph ->
       C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  Deduction
       form of ~ eleesub .  (Contributed by Scott Fenton, 17-Jul-2013.) $)
    eleesubd $p |- ( ( ph /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel w3a c1 cfz co cv cmin cmpt wa cr fveere wceq 3ad2ant1 wral
      resubcl syl2an anandirs ralrimiva wb cn eleenn mptelee syl adantr 3adant1
      mpbird eqeltrd ) ABFHIZJZCUQJZKDELFMNZEOZBIZVACIZPNZQZUQAURDVEUAUSGUBURUS
      VEUQJZAURUSRZVFVDSJZEUTUCZVGVHEUTURUSVAUTJZVHURVJRVBSJVCSJVHUSVJRBVAFTCVA
      FTVBVCUDUEUFUGURVFVIUHZUSURFUIJVKBFUJVBVCEPFUKULUMUOUNUP $.
      $( [17-Jul-2013] $)
  $}

