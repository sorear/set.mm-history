$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Tarskian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c EE Btwn Cgr $.

  $( Declare the syntax for the Euclidean space generator. $)
  cee $a class EE $.

  $( Declare the syntax for the Euclidean betweenness predicate. $)
  cbtwn $a class Btwn $.

  $( Declare the syntax for the Euclidean congruence predicate. $)
  ccgr $a class Cgr $.

  $( Define the Euclidean space generator.  For details, see ~ elee . $)
  df-ee $a |- EE = ( n e. NN |-> ( RR ^m ( 1 ... n ) ) ) $.

  ${
    $d n x y z t i $.
    $( Define the Euclidean betweenness predicate.  For details, see
       ~ brbtwn . $)
    df-btwn $a |- Btwn =
      `' { <. <. x , z >. , y >. |
         E. n e. NN (
           ( x e. ( EE ` n ) /\ z e. ( EE ` n ) /\ y e. ( EE ` n ) ) /\
           E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... n )
             ( y ` i ) = ( ( ( 1 - t ) x. ( x ` i ) ) + ( t x. ( z ` i ) ) ) )
         } $.
  $}

  ${
    $d n x y i $.
    $( Define the Euclidean congruence predicate.  For details, see
       ~ brcgr . $)
    df-cgr $a |- Cgr =
       { <. x , y >. |
         E. n e. NN
           ( ( x e. ( ( EE ` n ) X. ( EE ` n ) ) /\
             y e. ( ( EE ` n ) X. ( EE ` n ) ) ) /\
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` x ) ` i ) - ( ( 2nd ` x ) ` i ) ) ^ 2 ) =
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` y ) ` i ) - ( ( 2nd ` y ) ` i ) ) ^ 2 ) ) } $.
  $}

  ${
    $d N n $.
    $( Membership in a Euclidean space.  We define Euclidean space here using
       Cartesian coordinates over ` N ` space.  We later abstract away from
       this using Tarski's geometry axioms, so this exact definition is
       unimportant.  (Contributed by Scott Fenton, 3-Jun-2013.) $)
    elee $p |- ( N e. NN -> ( A e. ( EE ` N ) <-> A : ( 1 ... N ) --> RR ) ) $=
      ( vn cn wcel cee cfv cr c1 cfz co cmap wf cv wceq oveq2 oveq2d df-ee ovex
      fvmpt eleq2d reex elmap syl6bb ) BDEZABFGZEAHIBJKZLKZEUGHAMUEUFUHACBHICNZ
      JKZLKUHDFUIBOUJUGHLUIBIJPQCRHUGLSTUAHUGAUBIBJSUCUD $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N a k $.  $d A a $.  $d B a $.  $d F a $.
    $( A condition for a mapping to be an element of a Euclidean space.
       (Contributed by Scott Fenton, 7-Jun-2013.) $)
    mptelee $p |- ( N e. NN ->
      ( ( k e. ( 1 ... N ) |-> ( A F B ) ) e. ( EE ` N ) <->
        A. k e. ( 1 ... N ) ( A F B ) e. RR ) ) $=
      ( va cn wcel co cr wral wss cv a1i wi wal bicomi ra4 sylbi bitri cfz cmpt
      c1 cee cfv wf elee crn wfn df-f cvv ovex rgen eqid mpbi mpbiran wceq wrex
      mptfng cab rnmpt sseq1i abss hbre1 ax-17 hbim r19.23v albii ralcom4 clel2
      hbal biimpri syld ralrimi hbra1 eleq1a syl6 rexlimd alrimiv impbii syl6bb
      ) EGHCUCEUAIZABDIZUBZEUDUEHWBJWDUFZWCJHZCWBKZWDEUGWEWDUHZJLZWGWEWDWBUIZWI
      WBJWDUJWCUKHZCWBKWJWKCWBWKCMWBHZABDULZNUMCWBWCWDWDUNZUSUOUPWIFMZWCUQZCWBU
      RZFUTZJLZWGWHWRJCFWBWCWDWNVAVBWSWQWOJHZOZFPZWGWQFJVCXBWGXBWFCWBXACFWQWTCW
      PCWBVDWTCVEZVFVKXBWPWTOZCWBKZFPZWLWFOZXFXBXEXAFWPWTCWBVGVHQXFXDFPZCWBKZXG
      XIXFXDCFWBVIQXIWLXHWFXHCWBRXHWFOXIWFXHFWCJWMVJVLNVMSSVNWGXAFWGWPWTCWBWFCW
      BVOXCWGWLWFXDWFCWBRWCJWOVPVQVRVSVTTTTWA $.
      $( [7-Jun-2013] $)
  $}

  ${
    $( If ` A ` is in ` ( EE `` N ) ` , then ` N ` is a natural.  (Contributed
       by Scott Fenton, 1-Jul-2013.) $)
    eleenn $p |- ( A e. ( EE ` N ) -> N e. NN ) $=
      ( vn cee cfv wcel c0 wceq wn cn n0i cdm cr c1 cv cfz co cmap cvv syl cmpt
      df-ee dmeqi dmmptg ovex a1i mprg eqtri eleq2i notbii biimpri ndmfv con1i
      ) ABDEZFUNGHZIBJFZUNAKUPUOUPIZBDLZFZIZUOUTUQUSUPURJBURCJMNCOZPQZRQZUAZLZJ
      DVDCUBUCVCSFZVEJHCJCJVCSUDVFVAJFMVBRUEUFUGUHUIUJUKBDULTUMT $.
      $( [1-Jul-2013] $)
  $}

  $( The forward direction of ~ elee .  (Contributed by Scott Fenton,
     1-Jul-2013.) $)
  eleei $p |- ( A e. ( EE ` N ) -> A : ( 1 ... N ) --> RR ) $=
    ( cee cfv wcel c1 cfz co cr wf cn wb eleenn elee syl ibi ) ABCDEZFBGHIAJZQB
    KEQRLABMABNOP $.
    $( [1-Jul-2013] $)

  $( A point belongs to at most one Euclidean space.  (Contributed by Scott
     Fenton, 1-Jul-2013.) $)
  eedimeq $p |- ( ( A e. ( EE ` N ) /\ A e. ( EE ` M ) ) -> N = M ) $=
    ( cee cfv wcel wa c1 wceq cfz co cr wf eleei cdm sylan9req syl2an cn eleenn
    fdm cuz wb nnuz syl6eleq fzopth mpbid simprd ) ACDEFZABDEFZGZHHIZCBIZUJHCJK
    ZHBJKZIZUKULGZUHUMLAMZUNLAMZUOUIACNABNUQURUMAOUNUMLATUNLATPQUHCHUAEZFBRFUOU
    PUBUIUHCRUSACSUCUDABSRHBHCUEQUFUG $.
    $( [1-Jul-2013] $)

  ${
    $d N a n x y z i t $.  $d A x y z i t n $.  $d B x y z i t n $.
    $d C x y z i t n $.
    $( The binary relationship form of the betweenness predicate.  The
       statement ` A Btwn <. B , C >. ` should be informally read as " ` A `
       lies on a line segment between ` B ` and ` C ` .  This exact definition
       is abstracted away by Tarski's geometry axioms later on.  (Contributed
       by Scott Fenton, 3-Jun-2013.) $)
    brbtwn $p |- (
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
      ( A Btwn <. B , C >. <->
        E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... N )
           ( A ` i ) =
           ( ( ( 1 - t ) x. ( B ` i ) ) + ( t x. ( C ` i ) ) ) ) ) $=
      ( vn cv cfv wcel w3a c1 co cmul caddc wceq wral wrex wa cn vy vz vx cbtwn
      cop wbr cee cmin cfz cc0 cicc copab2 ccnv df-btwn breqi wb cvv opex mpan2
      brcnvg 3ad2ant1 df-br eleq1 3anbi1d fveq1 oveq2d oveq1d eqeq2d rexralbidv
      anbi12d rexbidv 3anbi2d 3anbi3d eqeq1d eloprabg simp1 ancoms syl2an oveq2
      wi eedimeq raleqdv syl biimpd expimpd a1d rexlimdv eleenn fveq2 3anbi123d
      eleq2d rcla4ev exp32 mpcom impbid bitrd 3comr syl5bb ) BCDUEZUDUFBWSUAHZG
      HZUGIZJZUBHZXBJZUCHZXBJZKZEHZXFIZLAHZUHMZXIWTIZNMZXKXIXDIZNMZOMZPZELXAUIM
      ZQAUJLUKMZRZSZGTRZUAUBUCULZUMZUFZBFUGIZJZCYGJZDYGJZKZXIBIZXLXICIZNMZXKXID
      IZNMZOMZPZELFUIMZQZAXTRZBWSUDYEUAUCUBAEGUNUOYKYFWSBYDUFZUUAYHYIYFUUBUPZYJ
      YHWSUQJUUCCDURBWSYGUQYDUTUSVAUUBWSBUEYDJZYKUUAWSBYDVBYIYJYHUUDUUAUPYIYJYH
      KZUUDCXBJZDXBJZBXBJZKZYREXSQZAXTRZSZGTRZUUAYCUUFXEXGKZXJYNXPOMZPZEXSQAXTR
      ZSZGTRUUFUUGXGKZXJYQPZEXSQAXTRZSZGTRUUMUAUBUCCDBYGYGYGWTCPZYBUURGTUVCXHUU
      NYAUUQUVCXCUUFXEXGWTCXBVCVDUVCXRUUPAEXTXSUVCXQUUOXJUVCXNYNXPOUVCXMYMXLNXI
      WTCVEVFVGVHVIVJVKXDDPZUURUVBGTUVDUUNUUSUUQUVAUVDXEUUGUUFXGXDDXBVCVLUVDUUP
      UUTAEXTXSUVDUUOYQXJUVDXPYPYNOUVDXOYOXKNXIXDDVEVFVFVHVIVJVKXFBPZUVBUULGTUV
      EUUSUUIUVAUUKUVEXGUUHUUFUUGXFBXBVCVMUVEUUTYRAEXTXSUVEXJYLYQXIXFBVEVNVIVJV
      KVOUUEUUMUUAUUEUULUUAGTUUEUULUUAVTXATJUUEUUIUUKUUAUUEUUISZUUKUUAUVFXAFPZU
      UKUUAUPUUEYIUUFUVGUUIYIYJYHVPUUFUUGUUHVPUUFYIUVGCFXAWAVQVRUVGUUJYTAXTUVGY
      REXSYSXAFLUIVSWBVKZWCWDWEWFWGFTJZUUEUUAUUMVTYIYJUVIYHCFWHVAUVIUUEUUAUUMUU
      LUUEUUASGFTUVGUUIUUEUUKUUAUVGUUFYIUUGYJUUHYHUVGXBYGCXAFUGWIZWKUVGXBYGDUVJ
      WKUVGXBYGBUVJWKWJUVHVJWLWMWNWOWPWQWRWPWR $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N n i x y $.  $d A n i x y $.  $d B n i x y $.  $d C n i x y $.
    $d D n i x y $.
    $( The binary relationship form of the congruence predicate.  The statement
       ` <. A , B >. Cgr <. C , D >. ` should be read informally as "the ` N `
       dimensional point ` A ` is as far from ` B ` as ` C ` is from ` D ` , or
       "the line segment ` A B ` is congruent to the line segment ` C D ` .
       This particular definition is encapsulated by Tarski's axioms later on.
       (Contributed by Scott Fenton, 3-Jun-2013.) $)
    brcgr $p |- ( ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
                    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. Cgr <. C , D >. <->
        sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) =
        sum_ i e. ( 1 ... N ) ( ( ( C ` i ) - ( D ` i ) ) ^ 2 ) ) ) $=
      ( vn cfv wcel wa co c1st c2nd cmin c2 cexp csu wceq cn fveq1d vx cop ccgr
      vy wbr cv cee cxp c1 cfz wrex eleq1 anbi1d fveq2 oveq12d oveq1d sumeq2sdv
      opex eqeq1d anbi12d rexbidv anbi2d eqeq2d df-cgr brab wb opelxpg simplbda
      adantll ad2ant2l simplrr eedimeq syl2anc adantlr oveq2 sumeq1d syl op1stg
      eqeq12d adantr op2ndg eqeqan12d ad2antrr bitrd biimpd rexlimdva wi eleenn
      expimpd ad2antll opelxpi anim12i biimpar jca xpeq12 eleq2d rcla4ev sylan2
      exp32 mpcom impbid syl5bb ) ABUBZCDUBZUCUEXCGUFZUGHZXFUHZIZXDXGIZJZUIXEUJ
      KZEUFZXCLHZHZXLXCMHZHZNKZOPKZEQZXKXLXDLHZHZXLXDMHZHZNKZOPKZEQZRZJZGSUKZAF
      UGHZIZBYJIZJZCYJIZDYJIZJZJZUIFUJKZXLAHZXLBHZNKZOPKZEQZYRXLCHZXLDHZNKZOPKZ
      EQZRZUAUFZXGIZUDUFZXGIZJZXKXLUUJLHZHZXLUUJMHZHZNKZOPKZEQZXKXLUULLHZHZXLUU
      LMHZHZNKZOPKZEQZRZJZGSUKXHUUMJZXSUVHRZJZGSUKYIUAUDXCXDUCABURCDURUUJXCRZUV
      JUVMGSUVNUUNUVKUVIUVLUVNUUKXHUUMUUJXCXGULUMUVNUVAXSUVHUVNXKUUTXREUVNUUSXQ
      OPUVNUUPXNUURXPNUVNXLUUOXMUUJXCLUNTUVNXLUUQXOUUJXCMUNTUOUPUQUSUTVAUULXDRZ
      UVMYHGSUVOUVKXJUVLYGUVOUUMXIXHUULXDXGULVBUVOUVHYFXSUVOXKUVGYEEUVOUVFYDOPU
      VOUVCYAUVEYCNUVOXLUVBXTUULXDLUNTUVOXLUVDYBUULXDMUNTUOUPUQVCUTVAUAUDEGVDVE
      YQYIUUIYQYHUUIGSYQXESIZJZXJYGUUIUVQXJJZYGUUIUVRYGYRXREQZYRYEEQZRZUUIUVRXE
      FRZYGUWAVFYQXJUWBUVPYQXJJDXFIZYOUWBYPXIUWCYMXHYOXIUWCYNYOXICXFIUWCCDXFXFY
      JVGVHVIVJYMYNYOXJVKDFXEVLVMVNUWBXSUVSYFUVTUWBXKYRXREXEFUIUJVOZVPUWBXKYRYE
      EUWDVPVSZVQYQUWAUUIVFUVPXJYMYPUVSUUCUVTUUHYMYRXRUUBEYMXQUUAOPYMXNYSXPYTNY
      KXNYSRYLYKXLXMAABYJVRTVTYMXLXOBABYJYJWATUOUPUQYPYRYEUUGEYPYDUUFOPYPYAUUDY
      CUUENYPXLXTCYNXTCRYOCDYJVRVTTYPXLYBDCDYJYJWATUOUPUQWBZWCWDWEWIWFFSIZYQUUI
      YIWGYOUWGYMYNDFWHWJUWGYQUUIYIYQUUIJZUWGXCYJYJUHZIZXDUWIIZJZUWAJZYIUWHUWLU
      WAYQUWLUUIYMUWJYPUWKABYJYJWKCDYJYJWKWLVTYQUWAUUIUWFWMWNYHUWMGFSUWBXJUWLYG
      UWAUWBXHUWJXIUWKUWBXGUWIXCUWBXFYJRZUWNXGUWIRXEFUGUNZUWOXFYJXFYJWOVMZWPUWB
      XGUWIXDUWPWPUTUWEUTWQWRWSWTXAXB $.
      $( [3-Jun-2013] $)
  $}

  $( The function value of a point is a real.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveere $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. RR ) $=
    ( cee cfv wcel c1 cfz co cr wf eleei ffvelrn sylan ) ACDEFGCHIZJAKBOFBAEJFA
    CLOJBAMN $.
    $( [10-Jun-2013] $)

  $( The function value of a point is a complex.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveecn $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. CC ) $=
    ( cee cfv wcel c1 cfz co wa fveere recnd ) ACDEFBGCHIFJBAEABCKL $.
    $( [10-Jun-2013] $)

  ${
    $d A i $.  $d B i $.  $d N i $.
    $( Two points are equal iff they agree in all dimensions.  (Contributed by
       Scott Fenton, 10-Jun-2013.) $)
    eqeefv $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
       ( A = B <-> A. i e. ( 1 ... N ) ( A ` i ) = ( B ` i ) ) ) $=
      ( cee cfv wcel c1 cfz co wfn wceq cv wral wb cr wf eleei ffn syl eqfnfv2
      syl2an ) ADEFZGZAHDIJZKZBUEKZABLCMZAFUHBFLCUENOBUCGZUDUEPAQUFADRUEPASTUIU
      EPBQUGBDRUEPBSTCUEABUAUB $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    $( Two points are equal iff the square of the distance between them is
       zero.  (Contributed by Scott Fenton, 10-Jun-2013.)  (Revised by Mario
       Carneiro, 22-May-2014.) $)
    eqeelen $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        ( A = B <->
          sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) = 0 ) ) $=
      ( cee cfv wcel wa co cc0 wceq c1 wral cc wb cr fveere syl2anc recnd syl
      cv cmin c2 cexp cfz csu adantlr adantll sqeq0 subeq0 bitrd ralbidva fzfid
      resubcl resqcl cle wbr sqge0 fsum00 eqeefv 3bitr4rd ) ADEFZGZBVBGZHZCUAZA
      FZVFBFZUBIZUCUDIZJKZCLDUEIZMVGVHKZCVLMVLVJCUFJKABKVEVKVMCVLVEVFVLGZHZVKVI
      JKZVMVOVINGVKVPOVOVIVOVGPGZVHPGZVIPGZVCVNVQVDAVFDQUGZVDVNVRVCBVFDQUHZVGVH
      UNRZSVIUITVOVGNGVHNGVPVMOVOVGVTSVOVHWASVGVHUJRUKULVEVLVJCVELDUMVOVSVJPGWB
      VIUOTVOVSJVJUPUQWBVIURTUSABCDUTVA $.
      $( [22-May-2014] $) $( [10-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( Alternate characterization of betweenness, with no existential
       quantifiers.  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    brbtwn2 $p |-
        ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( A Btwn <. B , C >. <->
            ( A. i e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 /\
              A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) ) $=
      ( wcel wbr c1 cmin co cmul caddc wceq cc0 cle wa cc syl2anc oveq1d vk cfv
      vt vp w3a cv wral wrex wi fveere 3ad2antl2 3ad2antl3 cneg resubcl 3adant3
      cr recnd syl oveq2d 3ad2ant3 sylancr mul4 syl22anc recn 3ad2ant1 3ad2ant2
      subdi syl3anc ax-1cn subdir mp3an1 mulid2 3eqtr3d remulcl subsub4 mulneg1
      1re simp1 3eqtrd eqtrd oveq12d wb subge0 mpbird mpbid eqbrtrd 3expa sylan
      mulge0 an32s ralrimiva fveecn anim12i anandirs subcl ancoms mulcom simp2r
      anandis simp2l simp3 mpan mulcl sub32 3eqtr3rd 3eqtr2d syl2an weq eqeq12d
      fveq2 rcla4cva oveq2 breq1d ralbidva oveqan12d wo 3ad2antl1 anbi1d simpl2
      anbi12d eqeefv adantlr fveq1 orbi12d bitrd ex adantr 3adant1 oveq1 eqeq2d
      ralbidv rcla4ev wne cdiv breq12d simprr simprl clt necomd syl112anc cbtwn
      cee cop cfz cicc brbtwn jca c2 cexp sqval 0re elicc2i simp1bi negcl nncan
      subsub3 negeqd readdcl negsubdi2 3eqtr2rd mulneg2 simp2bi simp3bi le0neg2
      simp2 resqcl sqge0 3adantl1 simp1l simp1r ralrimivva oveqan12rd 2ralbidva
      biimprcd rexlimdva mulsuble0b simpl1 letri3 pm4.25 breq2d anbi2d ad2antlr
      mulsub2 syl5bb biimprd adantrd 0elunit 3jca mul02 addid1 ad2antll subid1i
      eqtr2d syl6eq exp32 syldd wn necon3abid df-ne rexbii rexnal bitri syl6bbr
      rcla4v ad2antrl simpl lesub1 ltlen imp simplrr mpbir2and posdif divelunit
      letr div2sub syl23anc lesub2 eqeltrd jaodan rcla42v simp11 simp12 simpl23
      syld simp13 simpl21 simpl12 simpl22 simpl3 subeq0 necon3bid divdir adddir
      simpl13 npncan2 simpl11 add32 addid2 addsub nnncan2 eqtr3d eqtr4d 3eqtr4d
      addsubass simpr adddi npcan addcl divmul div23 divsubdir syl331anc 3expia
      divid com23 ralrimdv anim12d syl6 rexlimdv sylbid pm2.61dne impbid ) AFUU
      BUBZGZBVVCGZCVVCGZUEZABCUUCUUAHUAUFZAUBZIUCUFZJKZVVHBUBZLKZVVJVVHCUBZLKZM
      KZNZUAIFUUDKZUGZUCOIUUEKZUHZDUFZBUBZVWBAUBZJKZVWBCUBZVWDJKZLKZOPHZDVVRUGZ
      VWEEUFZCUBZVWKAUBZJKZLKZVWKBUBZVWMJKZVWGLKZNZEVVRUGDVVRUGZQZUCABCUAFUUFVV
      GVWAVXAVVGVVSVXAUCVVTVVGVVJVVTGZQZVWCVVKVWCLKZVVJVWFLKZMKZJKZVWFVXFJKZLKZ
      OPHZDVVRUGZVXGVWLVVKVWPLKZVVJVWLLKZMKZJKZLKZVWPVXNJKZVXHLKZNZEVVRUGDVVRUG
      ZVVSVXAUIVXCVXJDVVRVVGVWBVVRGZVXBVXJVVGVYAQZVWCUPGZVWFUPGZQVXBVXJVYBVYCVY
      DVVEVVDVYAVYCVVFBVWBFUJUKZVVFVVDVYAVYDVVECVWBFUJULZUUGVYCVYDVXBVXJVYCVYDV
      XBUEZVXIVVJVVKUMZLKZVWCVWFJKZUUHUUIKZLKZOPVYGVYLVYIVYJVYJLKZLKZVVJVYJLKZV
      YHVYJLKZLKZVXIVYGVYKVYMVYILVYGVYJRGZVYKVYMNVYGVYJVYCVYDVYJUPGZVXBVWCVWFUN
      UOZUQZVYJUUJURUSVYGVVJRGZVYRVYHRGZVYRVYQVYNNVXBVYCWUBVYDVXBVVJVXBVVJUPGZO
      VVJPHZVVJIPHZOIVVJUUKVQUULZUUMZUQZUTZWUAVYGVVKRGZWUCVYGVVKVXBVYCVVKUPGZVY
      DVXBIUPGZWUDWULVQWUHIVVJUNVAZUTZUQZVVKUUNURWUAVVJVYJVYHVYJVBVCVYGVYOVXGVY
      PVXHLVYGVYOVVJVWCLKZVXEJKZVWCVXDJKZVXEJKZVXGVYGWUBVWCRGZVWFRGZVYOWURNZWUJ
      VYCVYDWVAVXBVWCVDVEZVYDVYCWVBVXBVWFVDVFZVVJVWCVWFVGZVHVYGWUQWUSVXEJVYGIVV
      KJKZVWCLKZIVWCLKZVXDJKZWUQWUSVYGWUKWVAWVHWVJNZWUPWVDIRGZWUKWVAWVKVIIVVKVW
      CVJVKZSVYGWVGVVJVWCLVYGWVLWUBWVGVVJNZVIWUJIVVJUUOZVATVYGWVIVWCVXDJVYGWVAW
      VIVWCNZWVDVWCVLZURTVMTVYGWVAVXDRGZVXERGZWUTVXGNZWVDVYGVXDVYGWULVYCVXDUPGZ
      WUOVYCVYDVXBVRVVKVWCVNSZUQZVYGVXEVYGWUDVYDVXEUPGZVXBVYCWUDVYDWUHUTZVYCVYD
      VXBUVEVVJVWFVNSZUQZVWCVXDVXEVOZVHVSVYGVYPVVKVYJLKZUMZVXFVWFJKZUMZVXHVYGWU
      KVYRVYPWWJNWUPWUAVVKVYJVPSVYGWWIWWKVYGWWIVXDVVKVWFLKZJKZVXDVWFVXEJKZJKZWW
      KVYGWUKWVAWVBWWIWWNNWUPWVDWVEVVKVWCVWFVGVHVYGWWMWWOVXDJVYGWWMIVWFLKZVXEJK
      ZWWOVYGWUBWVBWWMWWRNZWUJWVEWVLWUBWVBWWSVIIVVJVWFVJVKZSVYGWWQVWFVXEJVYGWVB
      WWQVWFNZWVEVWFVLZURTVTUSVYGWVRWVBWVSWWPWWKNWWCWVEWWGVXDVWFVXEUUPVHVSUUQVY
      GVXFRGWVBWWLVXHNVYGVXFVYGWWAWWDVXFUPGWWBWWFVXDVXEUURSUQWVEVXFVWFUUSSVSWAU
      UTVYGVYLVVJVVKLKZVYKLKZUMZOPVYGVYLWXCUMZVYKLKZWXEVYGVYIWXFVYKLVYGWUBWUKVY
      IWXFNWUJWUPVVJVVKUVASTVYGWXCRGVYKRGWXGWXENVYGWXCVYGWUDWULWXCUPGZWWEWUOVVJ
      VVKVNSZUQVYGVYKVYGVYSVYKUPGZVYTVYJUVFURZUQWXCVYKVPSVTVYGOWXDPHZWXEOPHZVYG
      WXHOWXCPHZWXJOVYKPHZWXLWXIVXBVYCWXNVYDVXBWUDWUEWULOVVKPHZWXNWUHVXBWUDWUEW
      UFWUGUVBWUNVXBWXPWUFVXBWUDWUEWUFWUGUVCVXBWUMWUDWXPWUFWBVQWUHIVVJWCVAWDVVJ
      VVKWIVCUTWXKVYGVYSWXOVYTVYJUVGURWXCVYKWIVCVYGWXDUPGZWXLWXMWBVYGWXHWXJWXQW
      XIWXKWXCVYKVNSWXDUVDURWEWFWFWGWHWJWKVXCVXSDEVVRVVRVVGVYAVWKVVRGZQZVXBVXSV
      VGWXSQWVAWVBQZVWPRGZVWLRGZQZQZWUBVXSVXBVVEVVFWXSWYDVVDVVEVVFQZVYAWXRWYDWY
      EVYAQWXTWYEWXRQWYCVVEVVFVYAWXTVVEVYAQWVAVVFVYAQWVBBVWBFWLCVWBFWLWMWNVVEVV
      FWXRWYCVVEWXRQWYAVVFWXRQWYBBVWKFWLCVWKFWLWMWNWMWSUVHWUIWXTWYCWUBVXSWXTWYC
      WUBUEZWXCVYJVWLVWPJKZLKZLKZWXCVWPVWLJKZVWFVWCJKZLKZLKZVXPVXRWYFWYHWYLWXCL
      WYFWYHWYGVYJLKZWYLWYFVYRWYGRGZWYHWYNNWXTWYCVYRWUBVWCVWFWOVEZWYCWXTWYOWUBW
      YBWYAWYOVWLVWPWOWPVFZVYJWYGWQSWYFWYBWYAWVAWVBWYNWYLNWXTWYAWYBWUBWRZWXTWYA
      WYBWUBWTZWVAWVBWYCWUBUVIZWVAWVBWYCWUBUVJZVWLVWPVWCVWFUWCVCVTUSWYFWYIVYOVV
      KWYGLKZLKZVXPWYFWUBWUKVYRWYOWYIXUCNWXTWYCWUBXAZWUBWXTWUKWYCWVLWUBWUKVIIVV
      JWOXBUTZWYPWYQVVJVVKVYJWYGVBVCWYFVYOVXGXUBVXOLWYFVYOWURWUTVXGWYFWUBWVAWVB
      WVCXUDWYTXUAWVFVHWYFWUQWUSVXEJWYFWVHWVJWUQWUSWYFWUKWVAWVKXUEWYTWVMSWYFWVG
      VVJVWCLWUBWXTWVNWYCWVLWUBWVNVIWVOXBUTZTWYFWVIVWCVXDJWYFWVAWVPWYTWVQURTVMT
      WYFWVAWVRWVSWVTWYTWYFWUKWVAWVRXUEWYTVVKVWCXCSZWYFWUBWVBWVSXUDXUAVVJVWFXCS
      ZWWHVHVSWYFXUBVWLVXMJKZVXLJKZVWLVXLJKVXMJKZVXOWYFXUBVVKVWLLKZVXLJKZXUJWYF
      WUKWYBWYAXUBXUMNXUEWYRWYSVVKVWLVWPVGVHWYFXULXUIVXLJWYFXULIVWLLKZVXMJKZXUI
      WYFWUBWYBXULXUONZXUDWYRWVLWUBWYBXUPVIIVVJVWLVJVKSWYFXUNVWLVXMJWYFWYBXUNVW
      LNWYRVWLVLURTVTTVTWYFWYBVXMRGZVXLRGZXUJXUKNWYRWYFWUBWYBXUQXUDWYRVVJVWLXCS
      ZWYFWUKWYAXURXUEWYSVVKVWPXCSZVWLVXMVXLXDVHWYFWYBXURXUQXUKVXONWYRXUTXUSVWL
      VXLVXMVOVHVSWAVTWYFWYMVVJWYJLKZVVKWYKLKZLKZVXRWYFWUBWUKWYJRGZWYKRGZWYMXVC
      NXUDXUEWYCWXTXVDWUBVWPVWLWOVFWXTWYCXVEWUBWVBWVAXVEVWFVWCWOWPVEVVJVVKWYJWY
      KVBVCWYFXVAVXQXVBVXHLWYFXVAVVJVWPLKZVXMJKZVWPVXLJKZVXMJKZVXQWYFWUBWYAWYBX
      VAXVGNXUDWYSWYRVVJVWPVWLVGVHWYFXVHXVFVXMJWYFWVGVWPLKZIVWPLKZVXLJKZXVFXVHW
      YFWUKWYAXVJXVLNZXUEWYSWVLWUKWYAXVMVIIVVKVWPVJVKSWYFWVGVVJVWPLXUFTWYFXVKVW
      PVXLJWYFWYAXVKVWPNWYSVWPVLURTXETWYFWYAXURXUQXVIVXQNWYSXUTXUSVWPVXLVXMVOVH
      XFWYFXVBWWMVXDJKZWWOVXDJKZVXHWYFWUKWVBWVAXVBXVNNXUEXUAWYTVVKVWFVWCVGVHWYF
      WWMWWOVXDJWYFWWMWWRWWOWYFWUBWVBWWSXUDXUAWWTSWYFWWQVWFVXEJWYFWVBWXAXUAWXBU
      RTVTTWYFXVOVWFVXDJKVXEJKZVXHWYFWVBWVSWVRXVOXVPNXUAXUHXUGVWFVXEVXDXDVHWYFW
      VBWVRWVSXVPVXHNXUAXUGXUHVWFVXDVXEVOVHVTVSWAVTVMWGXGWJUVKVVSVXAVXKVXTQVVSV
      WJVXKVWTVXTVVSVWIVXJDVVRVVSVYAQZVWDVXFNZVWIVXJWBVVQXVRUAVWBVVRUADXHZVVIVW
      DVVPVXFVVHVWBAXJXVSVVMVXDVVOVXEMXVSVVLVWCVVKLVVHVWBBXJUSXVSVVNVWFVVJLVVHV
      WBCXJUSWAXIXKZXVRVWHVXIOPXVRVWEVXGVWGVXHLVWDVXFVWCJXLZVWDVXFVWFJXLZWAXMUR
      XNVVSVWSVXSDEVVRVVRVVSVYAWXRVWSVXSWBZXVQXVRVWMVXNNZXWCVVSWXRQXVTVVQXWDUAV
      WKVVRUAEXHZVVIVWMVVPVXNVVHVWKAXJXWEVVMVXLVVOVXMMXWEVVLVWPVVKLVVHVWKBXJUSX
      WEVVNVWLVVJLVVHVWKCXJUSWAXIXKXVRXWDQVWOVXPVWRVXRXVRXWDVWEVXGVWNVXOLXWAVWM
      VXNVWLJXLXOXWDXVRVWQVXQVWGVXHLVWMVXNVWPJXLXWBUVLXIXGWSUVMXTUVNSUVOVVGVXAV
      WCVWDPHZVWDVWFPHZQZVWFVWDPHZVWDVWCPHZQZXPZDVVRUGZVWTQZVWAVVGVWJXWMVWTVVGV
      WIXWLDVVRVYBVYCVWDUPGZVYDVWIXWLWBVYEVVDVVEVYAXWOVVFAVWBFUJXQZVYFVWCVWDVWF
      UVPVHXNXRVVGXWNVWAUIZBCVVGBCNZXWNBANZVWAVVGXWRXWNXWSUIVVGXWRQZXWMXWSVWTXW
      TXWSXWMXWTXWSVWCVWDNZDVVRUGZXWMXWTVVEVVDXWSXXBWBVVDVVEVVFXWRXSVVDVVEVVFXW
      RUVQBADFYASXWTXXAXWLDVVRXWTVYAQZXXAXWFXWJQZXWLXXCVYCXWOXXAXXDWBVVGVYAVYCX
      WRVYEYBVVGVYAXWOXWRXWPYBVWCVWDUVRSXXDXXDXXDXPZXXCXWLXXDUVSXWRXXEXWLWBVVGV
      YAXWRXXDXWHXXDXWKXWRXWJXWGXWFXWRVWCVWFVWDPVWBBCYCZUVTUWAXWRXWFXWIXWJXWRVW
      CVWFVWDPXXFXMXRYDUWBUWDYEXNYEUWEUWFYFVVGXWRXWSVWAVVGXWRXWSQZQZOVVTGVVIIVV
      LLKZOVVNLKZMKZNZUAVVRUGZVWAUWGXXHXXLUAVVRVVGVVHVVRGZXXGXXLVVGXXNQZVVIRGZV
      VLRGZVVNRGZUEZXXGXXLXXOXXPXXQXXRVVDVVEXXNXXPVVFAVVHFWLZXQVVEVVDXXNXXQVVFB
      VVHFWLZUKVVFVVDXXNXXRVVECVVHFWLZULUWHXXSXXGQXXKVVLVVIXXSXXKVVLNZXXGXXQXXR
      XYCXXPXXQXXRQXXKVVLOMKZVVLXXQXXRXXIVVLXXJOMVVLVLVVNUWIZXOXXQXYDVVLNXXRVVL
      UWJYGVTYHYGXWSVVLVVINXXSXWRVVHBAYCUWKUWMWHWJWKVVSXXMUCOVVTVVJONZVVQXXLUAV
      VRXYFVVPXXKVVIXYFVVMXXIVVOXXJMXYFVVKIVVLLXYFVVKIOJKIVVJOIJXLIVIUWLUWNTVVJ
      OVVNLYIWAYJYKYLVAUWOUWPVVGBCYMZUDUFZBUBZXYHCUBZYMZUDVVRUHZXWQVVGXYGXYIXYJ
      NZUDVVRUGZUWQZXYLVVGXYNBCVVEVVFXWRXYNWBVVDBCUDFYAYHUWRXYLXYMUWQZUDVVRUHXY
      OXYKXYPUDVVRXYIXYJUWSUWTXYMUDVVRUXAUXBUXCVVGXYKXWQUDVVRVVGXYHVVRGZXYKXWQV
      VGXYQXYKQZQZXWNXYHAUBZXYIJKZXYJXYIJKZYNKZVVTGZVVIIYUCJKZVVLLKZYUCVVNLKZMK
      ZNZUAVVRUGZQVWAXYSXWMYUDVWTYUJXYSXWMXYIXYTPHZXYTXYJPHZQZXYJXYTPHZXYTXYIPH
      ZQZXPZYUDXYQXWMYUQUIVVGXYKXWLYUQDXYHVVRDUDXHZXWHYUMXWKYUPYURXWFYUKXWGYULY
      URVWCXYIVWDXYTPVWBXYHBXJZVWBXYHAXJZYOYURVWDXYTVWFXYJPYUTVWBXYHCXJZYOXTYUR
      XWIYUNXWJYUOYURVWFXYJVWDXYTPYVAYUTYOYURVWDXYTVWCXYIPYUTYUSYOXTYDUXDUXEXYS
      YUQYUDXYSYUMYUDYUPXYSYUMQZYUDYUAYUBPHZYVBYULYVCXYSYUKYULYPXYSYULYVCWBZYUM
      XYSXYTUPGZXYJUPGZXYIUPGZYVDVVGVVDXYQYVEXYRVVDVVEVVFVRXYQXYKUXFZAXYHFUJXGZ
      VVGVVFXYQYVFXYRVVDVVEVVFXAYVHCXYHFUJXGZXYSVVEXYQYVGVVDVVEVVFXYRXSVVGXYQXY
      KYQBXYHFUJSZXYTXYJXYIUXGVHYGWEYVBYUAUPGZOYUAPHZYUBUPGZOYUBYRHZYUDYVCWBXYS
      YVLYUMXYSYVEYVGYVLYVIYVKXYTXYIUNSYGYVBYVMYUKXYSYUKYULYQXYSYVMYUKWBZYUMXYS
      YVEYVGYVPYVIYVKXYTXYIWCSYGWDXYSYVNYUMXYSYVFYVGYVNYVJYVKXYJXYIUNSYGYVBXYIX
      YJYRHZYVOYVBYVQXYIXYJPHZXYJXYIYMZXYSYVQYVRYVSQWBZYUMXYSYVGYVFYVTYVKYVJXYI
      XYJUXHSYGXYSYUMYVRXYSYVGYVEYVFYUMYVRUIYVKYVIYVJXYIXYTXYJUXNVHUXIYVBXYIXYJ
      VVGXYQXYKYUMUXJYSUXKXYSYVQYVOWBZYUMXYSYVGYVFYWAYVKYVJXYIXYJUXLSYGWEYUAYUB
      UXMVCWDXYSYUPQZYUCXYIXYTJKZXYIXYJJKZYNKZVVTXYSYUCYWENZYUPXYSXYTRGZXYIRGZX
      YJRGZYWHYVSYWFXYSXYTYVIUQZXYSXYIYVKUQZXYSXYJYVJUQZYWKXYSXYIXYJVVGXYQXYKYP
      YSXYTXYIXYJXYIUXOUXPYGYWBYWEVVTGZYWCYWDPHZYWBYUNYWNXYSYUNYUOYQXYSYUNYWNWB
      ZYUPXYSYVFYVEYVGYWOYVJYVIYVKXYJXYTXYIUXQVHYGWEYWBYWCUPGZOYWCPHZYWDUPGZOYW
      DYRHZYWMYWNWBXYSYWPYUPXYSYVGYVEYWPYVKYVIXYIXYTUNSYGYWBYWQYUOXYSYUNYUOYPXY
      SYWQYUOWBZYUPXYSYVGYVEYWTYVKYVIXYIXYTWCSYGWDXYSYWRYUPXYSYVGYVFYWRYVKYVJXY
      IXYJUNSYGYWBXYJXYIYRHZYWSYWBYXAXYJXYIPHZXYKXYSYXAYXBXYKQWBZYUPXYSYVFYVGYX
      CYVJYVKXYJXYIUXHSYGXYSYUPYXBXYSYVFYVEYVGYUPYXBUIYVJYVIYVKXYJXYTXYIUXNVHUX
      IVVGXYQXYKYUPUXJUXKXYSYXAYWSWBZYUPXYSYVFYVGYXDYVJYVKXYJXYIUXLSYGWEYWCYWDU
      XMVCWDUXRUXSYFUYDXYSVWTYUIUAVVRXYSXXNVWTYUIVVGXYRXXNVWTYUIUIVVGXYRXXNUEZV
      WTYWCVVNVVIJKZLKZVVLVVIJKZXYJXYTJKZLKZNZYUIYXEXYQXXNVWTYXKUIVVGXYQXYKXXNW
      TVVGXYRXXNXAZVWSYXKYWCVWNLKZVWQYXILKZNDEXYHVVHVVRVVRYURVWOYXMVWRYXNYURVWE
      YWCVWNLYURVWCXYIVWDXYTJYUSYUTWATYURVWGYXIVWQLYURVWFXYJVWDXYTJYVAYUTWAUSXI
      EUAXHZYXMYXGYXNYXJYXOVWNYXFYWCLYXOVWLVVNVWMVVIJVWKVVHCXJVWKVVHAXJZWAUSYXO
      VWQYXHYXILYXOVWPVVLVWMVVIJVWKVVHBXJYXPWATXIUXTSYXEXXPXXQXXRYWGYWHYWIYVSYX
      KYUIUIYXEVVDXXNXXPVVDVVEVVFXYRXXNUYAYXLXXTSYXEVVEXXNXXQVVDVVEVVFXYRXXNUYB
      YXLXYASYXEVVFXXNXXRVVDVVEVVFXYRXXNUYEYXLXYBSVVGXYRYWGXXNYWJUOVVGXYRYWHXXN
      YWKUOVVGXYRYWIXXNYWLUOYXEXYIXYJVVGXYQXYKXXNWRYSXXSYWGYWHYWIUEZYVSUEZYXKYU
      IYXRYXKQZYXIVVLLKZYUAVVNLKZMKZYUBYNKZYXTYUBYNKZYYAYUBYNKZMKZVVIYUHYXSYXTR
      GZYYARGZYUBRGZYUBOYMZYYCYYFNYXSYXIRGZXXQYYGYXSYWIYWGYYKYWGYWHYWIXXSYVSYXK
      UYCZYWGYWHYWIXXSYVSYXKUYFZXYJXYTWOSZXXPXXQXXRYXQYVSYXKUYGZYXIVVLXCSZYXSYU
      ARGZXXRYYHYXSYWGYWHYYQYYMYWGYWHYWIXXSYVSYXKUYHZXYTXYIWOSZXXPXXQXXRYXQYVSY
      XKUYNZYUAVVNXCSZYXSYWIYWHYYIYYLYYRXYJXYIWOSZYXSYYJYVSXXSYXQYVSYXKUYIYXSYU
      BOXYJXYIYXSYWIYWHYUBONXYJXYINWBYYLYYRXYJXYIUYJSUYKWDZYXTYYAYUBUYLYTYXSYYC
      VVINZYUBVVILKZYYBNZYXSUUUEYXGYXIVVILKZMKZYYAMKZYXJUUUGMKZYYAMKYYBYXSUUUEY
      WCVVNLKZUUUEMKZYYAMKZUUUIYXSUUUKYYAMKZUUUEMKZOUUUEMKZUUUMUUUEYXSUUUNOUUUE
      MYXSYWCYUAMKZVVNLKZXXJUUUNOYXSUUUQOVVNLYXSYWHYWGUUUQONYYRYYMXYIXYTUYOSTYX
      SYWCRGZYYQXXRUUURUUUNNYXSYWHYWGUUUSYYRYYMXYIXYTWOSZYYSYYTYWCYUAVVNUYMVHYX
      SXXRXXJONYYTXYEURVMTYXSUUUKRGZYYHUUUERGZUUUOUUUMNYXSUUUSXXRUUVAUUUTYYTYWC
      VVNXCSZUUUAYXSYYIXXPUUVBUUUBXXPXXQXXRYXQYVSYXKUYPZYUBVVIXCSZUUUKYYAUUUEUY
      QVHYXSUUVBUUUPUUUENUUVEUUUEUYRURXEYXSUUULUUUHYYAMYXSUUUKUUUGMKYWCVVILKZJK
      ZUUUKUUVFJKZUUUGMKZUUULUUUHYXSUUVAUUUGRGZUUVFRGZUUVGUUVINUUVCYXSYYKXXPUUV
      JYYNUUVDYXIVVIXCSZYXSUUUSXXPUUVKUUUTUUVDYWCVVIXCSZUUUKUUUGUUVFUYSVHYXSUUU
      LUUUKUUUGUUVFJKZMKZUUVGYXSUUUEUUVNUUUKMYXSYXIYWCJKZVVILKZUUUEUUVNYXSUUVPY
      UBVVILYXSYWIYWHYWGUUVPYUBNYYLYYRYYMXYJXYIXYTUYTVHTYXSYYKUUUSXXPUUVQUUVNNY
      YNUUUTUUVDYXIYWCVVIVJVHVUAUSYXSUUVAUUVJUUVKUUVGUUVONUUVCUUVLUUVMUUUKUUUGU
      UVFVUDVHVUBYXSYXGUUVHUUUGMYXSUUUSXXRXXPYXGUUVHNUUUTYYTUUVDYWCVVNVVIVGVHTV
      UCTVTYXSUUUHUUUJYYAMYXSYXGYXJUUUGMYXRYXKVUETTYXSUUUJYXTYYAMYXSUUUJYXIYXHL
      KZUUUGMKZYXIYXHVVIMKZLKZYXTYXSYXJUUVRUUUGMYXSYXHRGZYYKYXJUUVRNYXSXXQXXPUU
      WBYYOUUVDVVLVVIWOSZYYNYXHYXIWQSTYXSYYKUUWBXXPUUWAUUVSNYYNUUWCUUVDYXIYXHVV
      IVUFVHYXSUUVTVVLYXILYXSXXQXXPUUVTVVLNYYOUUVDVVLVVIVUGSUSXFTVSYXSYYBRGZXXP
      YYIYYJUUUDUUUFWBYXSYYGYYHUUWDYYPUUUAYXTYYAVUHSUUVDUUUBUUUCYYBVVIYUBVUIYTW
      DYXSYYDYUFYYEYUGMYXSYYDYXIYUBYNKZVVLLKZYUFYXSYYKXXQYYIYYJYYDUUWFNYYNYYOUU
      UBUUUCYXIVVLYUBVUJYTYXSUUWEYUEVVLLYXSYUBYUAJKZYUBYNKZYUBYUBYNKZYUCJKZUUWE
      YUEYXSYYIYYQYYIYYJUUWHUUWJNUUUBYYSUUUBUUUCYUBYUAYUBVUKYTYXSUUWGYXIYUBYNYX
      SYWIYWGYWHUUWGYXINYYLYYMYYRXYJXYTXYIUYTVHTYXSUUWIIYUCJYXSYYIYYJUUWIINUUUB
      UUUCYUBVUNSTVMTVTYXSYYQXXRYYIYYJYYEYUGNYYSYYTUUUBUUUCYUAVVNYUBVUJYTWAVMYF
      VULUYDVUMVUOVUPVUQVVSYUJUCYUCVVTVVJYUCNZVVQYUIUAVVRUUWKVVPYUHVVIUUWKVVMYU
      FVVOYUGMUUWKVVKYUEVVLLVVJYUCIJXLTVVJYUCVVNLYIWAYJYKYLVURUWOVUSVUTVVAVUTVV
      BYE $.
      $( [24-Jun-2013] $)
  $}


  $( Lemma for ~ colinearalg .  Expand out a multiplication. $)
  colinearalglem1 $p |- (
    ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
      ( D e. CC /\ E e. CC /\ F e. CC ) ) ->
    ( ( ( B - A ) x. ( F - D ) ) = ( ( E - D ) x. ( C - A ) ) <->
      ( ( B x. F ) - ( ( A x. F ) + ( B x. D ) ) ) =
      ( ( C x. E ) - ( ( A x. E ) + ( C x. D ) ) ) ) ) $=
    ( cc wcel cmin co cmul wceq caddc subcl syl2anc syl3anc mulcl syl2an subdir
    w3a wa simpl2 simpl1 simpr3 simpr1 subdi simp2 simp3 addsub oveq12d subsub3
    simp1 eqtr2d subsub4 oveq1d 3eqtr3d eqtrd simpr2 simpl3 mulcom 3eqtrd addcl
    eqeq12d wb addcan2 bitrd ) AGHZBGHZCGHZTZDGHZEGHZFGHZTZUAZBAIJZFDIJKJZEDIJZ
    CAIJZKJZLBFKJZAFKJZBDKJZMJZIJZADKJZMJZCEKJZAEKJZCDKJZMJZIJZWFMJZLZWEWLLZVOV
    QWGVTWMVOVQVPFKJZVPDKJZIJZWGVOVPGHZVMVKVQWRLVOVHVGWSVGVHVIVNUBZVGVHVIVNUCZB
    ANOVJVKVLVMUDZVJVKVLVMUEZVPFDUFPVOWAWBIJZWFMJWCIJZXDWCIJZWFMJZWRWGVOXDGHZWF
    GHZWCGHZXEXGLVOWAGHZWBGHZXHVJVHVMXKVNVGVHVIUGZVKVLVMUHZBFQRZVJVGVMXLVNVGVHV
    IULZXNAFQRZWAWBNOZVJVGVKXIVNXPVKVLVMULZADQRZVJVHVKXJVNXMXSBDQRZXDWFWCUIPVOW
    RXDWCWFIJZIJZXEVOWPXDWQYBIVOVHVGVMWPXDLWTXAXBBAFSPVOVHVGVKWQYBLWTXAXCBADSPU
    JVOXHXJXIYCXELXRYAXTXDWCWFUKPUMVOXFWEWFMVOXKXLXJXFWELXOXQYAWAWBWCUNPUOUPUQV
    OVTVSVRKJZVSEKJZVSDKJZIJZWMVOVRGHZVSGHZVTYDLVOVLVKYHVJVKVLVMURZXCEDNOVOVIVG
    YIVGVHVIVNUSZXACANOZVRVSUTOVOYIVLVKYDYGLYLYJXCVSEDUFPVOWHWIIJZWFMJWJIJZYMWJ
    IJZWFMJZYGWMVOYMGHZXIWJGHZYNYPLVOWHGHZWIGHZYQVJVIVLYSVNVGVHVIUHZVKVLVMUGZCE
    QRZVJVGVLYTVNXPUUBAEQRZWHWINOZXTVJVIVKYRVNUUAXSCDQRZYMWFWJUIPVOYGYMWJWFIJZI
    JZYNVOYEYMYFUUGIVOVIVGVLYEYMLYKXAYJCAESPVOVIVGVKYFUUGLYKXAXCCADSPUJVOYQYRXI
    UUHYNLUUEUUFXTYMWJWFUKPUMVOYOWLWFMVOYSYTYRYOWLLUUCUUDUUFWHWIWJUNPUOUPVAVCVO
    WEGHZWLGHZXIWNWOVDVOXKWDGHZUUIXOVOXLXJUUKXQYAWBWCVBOWAWDNOVOYSWKGHZUUJUUCVO
    YTYRUULUUDUUFWIWJVBOWHWKNOXTWEWLWFVEPVF $.
    $( [24-Jun-2013] $)

  ${
    $d A i j $.  $d B i j $.  $d C i j $.  $d N i j $.
    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem2 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( C ` i ) - ( B ` i ) ) x. ( ( A ` j ) - ( B ` j ) ) ) =
         ( ( ( C ` j ) - ( B ` j ) ) x. ( ( A ` i ) - ( B ` i ) ) ) ) ) $=
      ( cfv wcel cmin co cmul wceq cc fveecn syl2an caddc mulcl syl2anc syl3anc
      w3a cee cv c1 cfz wa simp1 simpl simp2 simp3 simpr subsub4 subsub3 eqcomd
      wb addcl oveq2d subcl subsub eqtrd 3eqtr2d addsub12 3eqtrd eqeq1d subadd2
      syl6bb addsub addcom oveq1d eqtr3d eqeq2d subeqrev syl22anc 3bitrd subadd
      eqcom syl5rbbr sub32 bitrd 3bitr3d colinearalglem1 3anrot syl2anb 3bitr4d
      syl33anc 2ralbidva ) AFUAGZHZBWFHZCWFHZTZDUBZBGZWKAGZIJEUBZCGZWNAGZIJKJWN
      BGZWPIJWKCGZWMIJKJLZWRWLIJWPWQIJKJWOWQIJWMWLIJKJLZDEUCFUDJZXAWJWKXAHZWNXA
      HZUEZUEWMMHZWLMHZWRMHZWPMHZWQMHZWOMHZWSWTUNWJWGXBXEXDWGWHWIUFZXBXCUGZAWKF
      NOWJWHXBXFXDWGWHWIUHZXLBWKFNOWJWIXBXGXDWGWHWIUIZXLCWKFNOWJWGXCXHXDXKXBXCU
      JZAWNFNOWJWHXCXIXDXMXOBWNFNOWJWIXCXJXDXNXOCWNFNOXEXFXGTZXHXIXJTZUEZWLWOKJ
      ZWMWOKJZWLWPKJZPJZIJWRWQKJZWMWQKJZWRWPKJZPJZIJLZYEYAYCPJIJZXTXSYDPJZIJZLZ
      WSWTXRYBXSIJZYDIJZYEYCIJZLZYAYJPJZYNLZYGYKXRYMYPYNXRYMXTXSIJZYAYDIJZPJZYA
      YRYDIJZPJZYPXRYMYBYIIJZXTYIYAIJZIJZYTXRYBMHZXSMHZYDMHZYMUUCLXRXTMHZYAMHZU
      UFXPXEXJUUIXQXEXFXGUFZXHXIXJUIZWMWOQOZXPXFXHUUJXQXEXFXGUHZXHXIXJUFZWLWPQO
      ZXTYAUORZXPXFXJUUGXQUUNUULWLWOQOZXPXEXIUUHXQUUKXHXIXJUHZWMWQQOZYBXSYDUKSX
      RUUIYIMHZUUJUUEUUCLUUMXRUUGUUHUVAUURUUTXSYDUORZUUPXTYIYAULSXRUUEXTXSYSIJZ
      IJZYTXRUUDUVCXTIXRUVCUUDXRUUGUUJUUHUVCUUDLUURUUPUUTXSYAYDULSUMUPXRUUIUUGY
      SMHZUVDYTLUUMUURXRUUJUUHUVEUUPUUTYAYDUQRXTXSYSURSUSUTXRYRMHZUUJUUHYTUUBLX
      RUUIUUGUVFUUMUURXTXSUQRUUPUUTYRYAYDVASXRUUAYJYAPXRUUIUUGUUHUUAYJLUUMUURUU
      TXTXSYDUKSUPVBVCXRYOYLYNYDPJZLZYLYFYCIJZLZYGXRYOUVGYLLZUVHXRYLMHZUUHYNMHZ
      YOUVKUNXRUUFUUGUVLUUQUURYBXSUQRUUTXRYEMHZYCMHZUVMXPXGXHUVNXQXEXFXGUIZUUOW
      RWPQOZXPXGXIUVOXQUVPUUSWRWQQOZYEYCUQRZYLYDYNVDSUVGYLVOVEXRUVGUVIYLXRYEYDP
      JZYCIJZUVGUVIXRUVNUUHUVOUWAUVGLUVQUUTUVRYEYDYCVFSXRUVTYFYCIXRUVNUUHUVTYFL
      UVQUUTYEYDVGRVHVIVJXRUUFUUGYFMHZUVOUVJYGUNUUQUURXRUUHUVNUWBUUTUVQYDYEUORU
      VRYBXSYFYCVKVLVMXRYQYJYHLZYKXRYQYJYNYAIJZLZUWCUWEUWDYJLZXRYQUWDYJVOXRUVMU
      UJYJMHZUWFYQUNUVSUUPXRUUIUVAUWGUUMUVBXTYIUQRYNYAYJVNSVPXRUWDYHYJXRUWDYEYA
      IJYCIJZYHXRUVNUVOUUJUWDUWHLUVQUVRUUPYEYCYAVQSXRUVNUUJUVOUWHYHLUVQUUPUVRYE
      YAYCUKSUSVJVRYJYHVOVEVSWMWLWRWPWQWOVTXPXFXGXETXIXJXHTWTYKUNXQXEXFXGWAXHXI
      XJWAWLWRWMWQWOWPVTWBWCWDWE $.
      $( [24-Jun-2013] $)

    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem3 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( A ` i ) - ( C ` i ) ) x. ( ( B ` j ) - ( C ` j ) ) ) =
         ( ( ( A ` j ) - ( C ` j ) ) x. ( ( B ` i ) - ( C ` i ) ) ) ) ) $=
      ( cee cfv wcel w3a cv cmin co cmul wceq c1 cfz wral colinearalglem2 wb
      3comr bitrd ) AFGHZIZBUCIZCUCIZJDKZBHZUGAHZLMEKZCHZUJAHZLMNMUJBHZULLMUGCH
      ZUILMNMOEPFQMZRDUORUNUHLMULUMLMNMUKUMLMUIUHLMNMOEUORDUORZUIUNLMUMUKLMNMUL
      UKLMUHUNLMNMOEUORDUORZABCDEFSUEUFUDUPUQTBCADEFSUAUB $.
      $( [24-Jun-2013] $)


  $}

  ${
    $d A i $.  $d C i $.  $d K i $.  $d N i $.
    $( Lemma for ~ colinearalg .  Prove a disjunction that will be needed in
       the final proof. $)
    colinearalglem4 $p |- (
       ( ( A e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\ K e. RR ) ->
       ( A. i e. ( 1 ... N ) ( ( ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) +
         ( A ` i ) ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( C ` i ) - ( ( K x. ( ( C ` i ) -
         ( A ` i ) ) ) + ( A ` i ) ) ) x. ( ( A ` i ) -
         ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( A ` i ) - ( C ` i ) ) x. ( ( ( K x.
         ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) - ( C ` i ) ) ) <_ 0 ) ) $=
      ( wcel wa cr cc0 cle wbr c1 cmin co cmul cc wceq recnd syl2anc syl cee cv
      cfv w3o cfz wral relin01 adantl fveere adantlr adantll jca c2 cexp simprl
      caddc resubcl ancoms adantr mulass syl3anc remulcl ad2antrr oveq1d oveq2d
      recn pncan sqval 3eqtr4d wo simprr sqge0 wb resqcl mulle0b mpbird eqbrtrd
      orcd sylan an32s ralrimiva expr cneg ad2antlr ax-1cn subdir mp3an1 mulid2
      sub32 eqtr2d subsub4 3eqtr3rd df-neg syl6eqr oveq12d 1re ad2antrl mulneg2
      subid mpan mul4 negeqd 3eqtrd simpl sylancr subge0 biimpar adantrl mulge0
      syl22anc breqtrd le0neg2 negsubdi2 simplr simpll peano2rem mulneg1 eqtr4d
      mpbid mul12 eqtrd mp3an2 subsub3 mpan2 3orim123d mpd ) AEUAUCZFZBYGFZGZDH
      FZGZDIJKZIDJKZDLJKZGZLDJKZUDZDCUBZBUCZYSAUCZMNZONZUUAUPNZUUAMNZUUBONZIJKZ
      CLEUENZUFZYTUUDMNZUUAUUDMNZONZIJKZCUUHUFZUUAYTMNZUUDYTMNZONZIJKZCUUHUFZUD
      YKYRYJDUGUHYLYMUUIYPUUNYQUUSYJYKYMUUIYJYKYMGZGUUGCUUHYJYSUUHFZUUTUUGYJUVA
      GZUUAHFZYTHFZGZUUTUUGUVBUVCUVDYHUVAUVCYIAYSEUIUJYIUVAUVDYHBYSEUIUKULZUVEU
      UTGZUUFDUUBUMUNNZONZIJUVGUUCUUBONZDUUBUUBONZONZUUFUVIUVGDPFZUUBPFZUVNUVJU
      VLQUVGDUVEYKYMUOZRUVGUUBUVEUUBHFZUUTUVDUVCUVPYTUUAUQZURZUSZRZUVTDUUBUUBUT
      VAUVGUUEUUCUUBOUVGUUCPFZUUAPFZUUEUUCQUVGUUCUVGYKUVPUUCHFZUVOUVSDUUBVBZSRU
      VCUWBUVDUUTUUAVFZVCUUCUUAVGSVDUVGUVHUVKDOUVGUVNUVHUVKQZUVTUUBVHZTVEVIUVGU
      VIIJKZYMIUVHJKZGZYNUVHIJKGZVJZUVGUWJUWKUVGYMUWIUVEYKYMVKUVGUVPUWIUVSUUBVL
      ZTULVRUVGYKUVHHFZUWHUWLVMUVOUVGUVPUWNUVSUUBVNZTDUVHVOSVPVQVSVTWAWBYJYKYPU
      UNYJYKYPGZGUUMCUUHYJUVAUWPUUMUVBUVEUWPUUMUVFUVEUWPGZUULLDMNZDONZUVKONZWCZ
      IJUWQUULUWRUUBONZUUCWCZONZUXBUUCONZWCZUXAUWQUUJUXBUUKUXCOUWQUUBUUCMNZYTUU
      CMNUUAMNZUXBUUJUWQYTPFZUWBUWAUXGUXHQUVDUXIUVCUWPYTVFZWDZUVCUWBUVDUWPUWEVC
      ZUWQUUCUWQYKUVPUWCUVEYKYPUOZUVEUVPUWPUVRUSZUWDSRZYTUUAUUCWIVAUWQUXBLUUBON
      ZUUCMNZUXGUWQUVMUVNUXBUXQQZUWQDUXMRZUWQUUBUXNRZLPFZUVMUVNUXRWELDUUBWFWGSU
      WQUXPUUBUUCMUWQUVNUXPUUBQZUXTUUBWHZTVDWJUWQUXIUWAUWBUXHUUJQUXKUXOUXLYTUUC
      UUAWKVAWLUWQUUAUUAMNZUUCMNZUUAUUCMNUUAMNZUXCUUKUWQUWBUWBUWAUYEUYFQUXLUXLU
      XOUUAUUAUUCWIVAUWQUYEIUUCMNUXCUWQUYDIUUCMUWQUWBUYDIQUXLUUAWSTVDUUCWMWNUWQ
      UWBUWAUWBUYFUUKQUXLUXOUXLUUAUUCUUAWKVAWLWOUWQUXBPFUWAUXDUXFQUWQUXBUWQUWRH
      FZUVPUXBHFYKUYGUVEYPLHFZYKUYGWPLDUQZWTWQZUXNUWRUUBVBSRUXOUXBUUCWRSUWQUXEU
      WTUWQUWRPFUVNUVMUVNUXEUWTQUWQUWRUYJRUXTUXSUXTUWRUUBDUUBXAXJXBXCUWQIUWTJKZ
      UXAIJKZUWQIUWSUVHONZUWTJUWQUWSHFZIUWSJKZUWNUWIIUYMJKUWQUYGYKUYNUYJUXMUWRD
      VBSZUWPUYOUVEUWPUYGIUWRJKZYKYNUYOUWPUYHYKUYGWPYKYPXDZUYIXEYKYOUYQYNYKUYQY
      OUYHYKUYQYOVMWPLDXFWTXGXHUYRYKYNYOUOUWRDXIXJUHUWQUVPUWNUXNUWOTUWQUVPUWIUX
      NUWMTUWSUVHXIXJUWQUVHUVKUWSOUWQUVNUWFUXTUWGTVEXKUWQUWTHFZUYKUYLVMUWQUYNUV
      KHFZUYSUYPUWQUVPUVPUYTUXNUXNUUBUUBVBSUWSUVKVBSUWTXLTXSVQVSVTWAWBYJYKYQUUS
      YJYKYQGZGUURCUUHYJUVAVUAUURUVBUVEVUAUURUVFUVEVUAGZUUQDLMNZUVHONZWCZIJVUBU
      UBWCZVUCUUBONZONZUUOVUGONVUEUUQVUBVUFUUOVUGOVUBUXIUWBVUFUUOQUVDUXIUVCVUAU
      XJWDZUVCUWBUVDVUAUWEVCZYTUUAXMSVDVUBVUHUUBVUGONZWCZVUEVUBUVNVUGPFVUHVULQV
      UBUUBVUBUVDUVCUVPUVCUVDVUAXNUVCUVDVUAXOUVQSZRZVUBVUGVUBVUCHFZUVPVUGHFYKVU
      OUVEYQDXPWQZVUMVUCUUBVBSRUUBVUGXQSVUBVUKVUDVUBVUKVUCUVKONZVUDVUBUVNVUCPFU
      VNVUKVUQQVUNVUBVUCVUPRVUNUUBVUCUUBXTVAVUBUVHUVKVUCOVUBUVNUWFVUNUWGTVEXRXB
      YAVUBVUGUUPUUOOVUBVUGUUCUXPMNZUUCUUBMNZUUPVUBUVMUVNVUGVURQZVUBDUVEYKYQUOZ
      RVUNUVMUYAUVNVUTWEDLUUBWFYBSVUBUXPUUBUUCMVUBUVNUYBVUNUYCTVEVUBUWAUXIUWBVU
      SUUPQVUBUUCVUBYKUVPUWCVVAVUMUWDSRVUIVUJUUCYTUUAYCVAXCVEWLVUBIVUDJKZVUEIJK
      ZVUBVUOIVUCJKZUWNUWIVVBVUPVUBVVDYQUVEYKYQVKYKVVDYQVMZUVEYQYKUYHVVEWPDLXFY
      DWQVPVUBUVPUWNVUMUWOTZVUBUVPUWIVUMUWMTVUCUVHXIXJVUBVUDHFZVVBVVCVMVUBVUOUW
      NVVGVUPVVFVUCUVHVBSVUDXLTXSVQVSVTWAWBYEYF $.
      $( [27-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( An algebraic characterization of colinearity.  Note the similarity to
       ~ brbtwn2 .  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    colinearalg $p |-
        (
          ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( ( A Btwn <. B , C >. \/ B Btwn <. C , A >. \/
              C Btwn <. A , B >. ) <->
            A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) $=
      ( cfv wcel wbr cmin co cmul cc0 cle wral wceq wa wb cc syl2anc vp cee w3a
      cop cbtwn w3o cv cfz brbtwn2 3comr colinearalglem3 anbi2d colinearalglem2
      c1 bitrd 3coml 3orbi123d fveecn subid oveq2d adantl subcl mul01 syl eqtrd
      syl2an anandirs 0re leidi syl6eqbr ralrimiva 3adant1 fveq1 oveq12d breq1d
      wi ralbidv syl5ibcom 3mix1 syl6 a1dd wne wn simp3 simp1 eqeefv necon3abid
      wrex df-ne rexbii rexnal bitr2i syl6bb ralcom fveq2 oveq1d eqeq12d rcla4v
      weq ad2antrl fveere 3ad2antl1 3ad2antl2 3ad2antl3 3jca anim1i anasss cdiv
      caddc adantlr recn 3anim123i adantr ad2antlr simplrr simp12 simp11 simp22
      eqcom simp21 simp23 simpr3 simpr1 subeq0 necon3bid biimp3ar divcl syl3anc
      cr mulcl syl112anc syl5bb resubcl oveq1 ralimi ralbi oveq2 andir df-3or
      wo simp13 subadd2 bicomd div23 eqeq2d divmul mulcom eqeq1d 3bitr2d 3simpb
      ralbidva simpl2 simpl1 simpl3 3ad2ant1 biimpar colinearalglem4 syl5ibrcom
      recnd redivcl syldan syld syl5bi exp32 rexlimdv sylbid pm2.61dne pm4.71rd
      sylbird orbi1i anbi1i bitri 3bitr4i syl6rbb ) AFUBGZHZBUVOHZCUVOHZUCZABCU
      DUEIZBCAUDUEIZCABUDUEIZUFDUGZBGZUWCAGZJKZUWCCGZUWEJKZLKZMNIZDUNFUHKZOZUWF
      EUGZCGZUWMAGZJKZLKZUWMBGZUWOJKZUWHLKZPZEUWKODUWKOZQZUWGUWDJKZUWEUWDJKZLKZ
      MNIZDUWKOZUXBQZUWEUWGJKZUWDUWGJKZLKZMNIZDUWKOZUXBQZUFZUXBUVSUVTUXCUWAUXIU
      WBUXOABCDEFUIUVSUWAUXHUXDUWOUWRJKLKUWNUWRJKUXELKPEUWKODUWKOZQZUXIUVQUVRUV
      PUWAUXRRBCADEFUIUJUVSUXQUXBUXHUVQUVRUVPUXQUXBRBCADEFUKUJULUOUVRUVPUVQUWBU
      XORUVRUVPUVQUCZUWBUXNUXJUWRUWNJKLKUWOUWNJKUXKLKPEUWKODUWKOZQUXOCABDEFUIUX
      SUXTUXBUXNCABDEFUMULUOUPUQUVSUXBUWLUXHUXNUFZUXBQZUXPUVSUXBUYAUVSUXBUYAVPZ
      CAUVSCAPZUYAUXBUVSUYDUWLUYAUVSUXKUWGUWGJKZLKZMNIZDUWKOZUYDUWLUVQUVRUYHUVP
      UVQUVRQZUYGDUWKUYIUWCUWKHZQUYFMMNUVQUVRUYJUYFMPZUVQUYJQUWDSHZUWGSHZUYKUVR
      UYJQBUWCFURZCUWCFURZUYLUYMQZUYFUXKMLKZMUYMUYFUYQPUYLUYMUYEMUXKLUWGUSUTVAU
      YPUXKSHUYQMPUWDUWGVBUXKVCVDVEVFVGMVHVIVJVKVLUYDUYGUWJDUWKUYDUYFUWIMNUYDUX
      KUWFUYEUWHLUYDUWGUWEUWDJUWCCAVMZUTUYDUWGUWEUWGJUYRUTVNVOVQVRUWLUXHUXNVSVT
      WAUVSCAWBZUAUGZCGZUYTAGZWBZUAUWKWHZUYCUVSUYSVUAVUBPZUAUWKOZWCZVUDUVSVUFCA
      UVSUVRUVPUYDVUFRUVPUVQUVRWDUVPUVQUVRWECAUAFWFTWGVUDVUEWCZUAUWKWHVUGVUCVUH
      UAUWKVUAVUBWIWJVUEUAUWKWKWLWMUVSVUCUYCUAUWKUVSUYTUWKHZVUCUYCUXBUXADUWKOZE
      UWKOZUVSVUIVUCQZQZUYAUXADEUWKUWKWNVUMVUKUWFVUAVUBJKZLKZUYTBGZVUBJKZUWHLKZ
      PZDUWKOZUYAVUIVUKVUTVPUVSVUCVUJVUTEUYTUWKEUAWSZUXAVUSDUWKVVAUWQVUOUWTVURV
      VAUWPVUNUWFLVVAUWNVUAUWOVUBJUWMUYTCWOUWMUYTAWOZVNUTVVAUWSVUQUWHLVVAUWRVUP
      UWOVUBJUWMUYTBWOVVBVNWPWQVQWRWTUVSVULVUBYIHZVUPYIHZVUAYIHZUCZVUCQZVUTUYAV
      PUVSVUIVUCVVGUVSVUIQZVVFVUCVVHVVCVVDVVEUVPUVQVUIVVCUVRAUYTFXAXBUVQUVPVUIV
      VDUVRBUYTFXAXCUVRUVPVUIVVEUVQCUYTFXAXDXEXFXGUVSVVGQZVUTUWDVUQVUNXHKZUWHLK
      ZUWEXIKZPZDUWKOZUYAVVIVVMVUSDUWKVVIUYJQUWESHZUYLUYMUCZVUBSHZVUPSHZVUASHZU
      CZVUCVVMVUSRUVSUYJVVPVVGUVSUYJQVVOUYLUYMUVPUVQUYJVVOUVRAUWCFURXBUVQUVPUYJ
      UYLUVRUYNXCUVRUVPUYJUYMUVQUYOXDXEXJVVGVVTUVSUYJVVFVVTVUCVVCVVQVVDVVRVVEVV
      SVUBXKZVUPXKVUAXKXLXMXNUVSVVFVUCUYJXOVVMVVLUWDPZVVPVVTVUCUCZVUSUWDVVLXSVW
      CVWBUWFVVKPZUWFVURVUNXHKZPZVUSVWCUYLVVOVVKSHZVWBVWDRVVOUYLUYMVVTVUCXPZVVO
      UYLUYMVVTVUCXQZVWCVVJSHZUWHSHZVWGVWCVUQSHZVUNSHZVUNMWBZVWJVWCVVRVVQVWLVVP
      VVQVVRVVSVUCXRVVPVVQVVRVVSVUCXTZVUPVUBVBTZVWCVVSVVQVWMVVPVVQVVRVVSVUCYAVW
      OVUAVUBVBTZVVPVVTVWNVUCVVPVVTQZVUNMVUAVUBVWRVVSVVQVUNMPVUERZVVPVVQVVRVVSY
      BVVPVVQVVRVVSYCVUAVUBYDZTYEYFZVUQVUNYGYHVWCUYMVVOVWKVVOUYLUYMVVTVUCUUAVWI
      UWGUWEVBTZVVJUWHYJTUYLVVOVWGUCVWDVWBUWDUWEVVKUUBUUCYHVWCVWEVVKUWFVWCVWLVW
      KVWMVWNVWEVVKPVWPVXBVWQVXAVUQUWHVUNUUDYKUUEVWFVWEUWFPZVWCVUSUWFVWEXSVWCVX
      CVUNUWFLKZVURPZVUSVWCVURSHZUWFSHZVWMVWNVXCVXERVWCVWLVWKVXFVWPVXBVUQUWHYJT
      VWCUYLVVOVXGVWHVWIUWDUWEVBTZVWQVXAVURUWFVUNUUFYKVWCVXDVUOVURVWCVWMVXGVXDV
      UOPVWQVXHVUNUWFUUGTUUHUOYLUUIYLYHUUKUVSUVPUVRQZVVJYIHZVVNUYAVPVVGUVPUVQUV
      RUUJVVGVUQYIHZVUNYIHZVWNVXJVVGVVDVVCVXKVVCVVDVVEVUCUULVVCVVDVVEVUCUUMZVUP
      VUBYMTVVGVVEVVCVXLVVCVVDVVEVUCUUNVXMVUAVUBYMTVVFVWNVUCVVFVUNMVUAVUBVVFVVS
      VVQVWSVVFVUAVVCVVDVVEWDUUSVVCVVDVVQVVEVWAUUOVWTTYEUUPVUQVUNUUTYHVXIVXJQUY
      AVVNVVLUWEJKZUWHLKZMNIZDUWKOZUWGVVLJKZUWEVVLJKZLKZMNIZDUWKOZUXJVVLUWGJKZL
      KZMNIZDUWKOZUFACDVVJFUUQVVNUWLVXQUXHVYBUXNVYFVVNUWJVXPRZDUWKOUWLVXQRVVMVY
      GDUWKVVMUWIVXOMNVVMUWFVXNUWHLUWDVVLUWEJYNWPVOYOUWJVXPDUWKYPVDVVNUXGVYARZD
      UWKOUXHVYBRVVMVYHDUWKVVMUXFVXTMNVVMUXDVXRUXEVXSLUWDVVLUWGJYQUWDVVLUWEJYQV
      NVOYOUXGVYADUWKYPVDVVNUXMVYERZDUWKOUXNVYFRVVMVYIDUWKVVMUXLVYDMNVVMUXKVYCU
      XJLUWDVVLUWGJYNUTVOYOUXMVYEDUWKYPVDUQUURVFUVIUVAUVBUVCUVDUVEUVFUVGUVHUWLU
      XHYTZUXBQZUXOYTZUXCUXIYTZUXOYTUYBUXPVYKVYMUXOUWLUXHUXBYRUVJUYBVYJUXNYTZUX
      BQVYLUYAVYNUXBUWLUXHUXNYSUVKVYJUXNUXBYRUVLUXCUXIUXOYSUVMUVNUO $.
      $( [24-Jun-2013] $)
  $}


  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesub.1 $e |- C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  (Contributed
       by Scott Fenton, 17-Jul-2013.) $)
    eleesub $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel wa c1 cfz co cv cmin cmpt cr wral fveere resubcl ralrimiva
      syl2an anandirs wb cn eleenn mptelee syl adantr mpbird syl5eqel ) AEGHZIZ
      BULIZJZCDKELMZDNZAHZUQBHZOMZPZULFUOVAULIZUTQIZDUPRZUOVCDUPUMUNUQUPIZVCUMV
      EJURQIUSQIVCUNVEJAUQESBUQESURUSTUBUCUAUMVBVDUDZUNUMEUEIVFAEUFURUSDOEUGUHU
      IUJUK $.
      $( [17-Jul-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesubd.1 $e |- ( ph ->
       C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  Deduction
       form of ~ eleesub .  (Contributed by Scott Fenton, 17-Jul-2013.) $)
    eleesubd $p |- ( ( ph /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel w3a c1 cfz co cv cmin cmpt wa cr fveere wceq 3ad2ant1 wral
      resubcl syl2an anandirs ralrimiva wb cn eleenn mptelee syl adantr 3adant1
      mpbird eqeltrd ) ABFHIZJZCUQJZKDELFMNZEOZBIZVACIZPNZQZUQAURDVEUAUSGUBURUS
      VEUQJZAURUSRZVFVDSJZEUTUCZVGVHEUTURUSVAUTJZVHURVJRVBSJVCSJVHUSVJRBVAFTCVA
      FTVBVCUDUEUFUGURVFVIUHZUSURFUIJVKBFUJVBVCEPFUKULUMUOUNUP $.
      $( [17-Jul-2013] $)
  $}

