$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Tarskian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c EE Btwn Cgr $.

  $( Declare the syntax for the Euclidean space generator. $)
  cee $a class EE $.

  $( Declare the syntax for the Euclidean betweenness predicate. $)
  cbtwn $a class Btwn $.

  $( Declare the syntax for the Euclidean congruence predicate. $)
  ccgr $a class Cgr $.

  $( Define the Euclidean space generator.  For details, see ~ elee . $)
  df-ee $a |- EE = ( n e. NN |-> ( RR ^m ( 1 ... n ) ) ) $.

  ${
    $d n x y z t i $.
    $( Define the Euclidean betweenness predicate.  For details, see
       ~ brbtwn . $)
    df-btwn $a |- Btwn =
      `' { <. <. x , z >. , y >. |
         E. n e. NN (
           ( x e. ( EE ` n ) /\ z e. ( EE ` n ) /\ y e. ( EE ` n ) ) /\
           E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... n )
             ( y ` i ) = ( ( ( 1 - t ) x. ( x ` i ) ) + ( t x. ( z ` i ) ) ) )
         } $.
  $}

  ${
    $d n x y i $.
    $( Define the Euclidean congruence predicate.  For details, see
       ~ brcgr . $)
    df-cgr $a |- Cgr =
       { <. x , y >. |
         E. n e. NN
           ( ( x e. ( ( EE ` n ) X. ( EE ` n ) ) /\
             y e. ( ( EE ` n ) X. ( EE ` n ) ) ) /\
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` x ) ` i ) - ( ( 2nd ` x ) ` i ) ) ^ 2 ) =
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` y ) ` i ) - ( ( 2nd ` y ) ` i ) ) ^ 2 ) ) } $.
  $}

  ${
    $d N n $.
    $( Membership in a Euclidean space.  We define Euclidean space here using
       Cartesian coordinates over ` N ` space.  We later abstract away from
       this using Tarski's geometry axioms, so this exact definition is
       unimportant.  (Contributed by Scott Fenton, 3-Jun-2013.) $)
    elee $p |- ( N e. NN -> ( A e. ( EE ` N ) <-> A : ( 1 ... N ) --> RR ) ) $=
      ( vn cn wcel cee cfv cr c1 cfz co cmap wf wceq opreq2 opreq2d df-ee oprex
      cv fvmpt eleq2d reex elmap syl6bb ) BDEZABFGZEAHIBJKZLKZEUGHAMUEUFUHACBHI
      CSZJKZLKUHDFUIBNUJUGHLUIBIJOPCQHUGLRTUAHUGAUBIBJRUCUD $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N a k $.  $d A a $.  $d B a $.  $d F a $.
    $( A condition for a mapping to be an element of a Euclidean space.
       (Contributed by Scott Fenton, 7-Jun-2013.) $)
    mptelee $p |- ( N e. NN ->
      ( ( k e. ( 1 ... N ) |-> ( A F B ) ) e. ( EE ` N ) <->
        A. k e. ( 1 ... N ) ( A F B ) e. RR ) ) $=
      ( va cn wcel co cr wral wss cv a1i wi wal bicomi ra4 sylbi bitri cfz cmpt
      c1 cee cfv elee crn wfn df-f cvv oprex rgen eqid mptfng mpbi mpbiran wceq
      wf wrex cab rnmpt sseq1i abss hbre1 ax-17 hbim hbal r19.23v albii ralcom4
      clel2 biimpri syld ralrimi hbra1 eleq1a rexlimd alrimiv impbii syl6bb
      syl6 ) EGHCUCEUAIZABDIZUBZEUDUEHWBJWDURZWCJHZCWBKZWDEUFWEWDUGZJLZWGWEWDWB
      UHZWIWBJWDUIWCUJHZCWBKWJWKCWBWKCMWBHZABDUKZNULCWBWCWDWDUMZUNUOUPWIFMZWCUQ
      ZCWBUSZFUTZJLZWGWHWRJCFWBWCWDWNVAVBWSWQWOJHZOZFPZWGWQFJVCXBWGXBWFCWBXACFW
      QWTCWPCWBVDWTCVEZVFVGXBWPWTOZCWBKZFPZWLWFOZXFXBXEXAFWPWTCWBVHVIQXFXDFPZCW
      BKZXGXIXFXDCFWBVJQXIWLXHWFXHCWBRXHWFOXIWFXHFWCJWMVKVLNVMSSVNWGXAFWGWPWTCW
      BWFCWBVOXCWGWLWFXDWFCWBRWCJWOVPWAVQVRVSTTTVT $.
      $( [7-Jun-2013] $)
  $}

  ${
    $( If ` A ` is in ` ( EE `` N ) ` , then ` N ` is a natural.  (Contributed
       by Scott Fenton, 1-Jul-2013.) $)
    eleenn $p |- ( A e. ( EE ` N ) -> N e. NN ) $=
      ( vn cee cfv wcel c0 wceq wn cn n0i cdm cr c1 cv cfz co cmap cvv syl cmpt
      df-ee dmeqi dmmptg oprex a1i mprg eqtri eleq2i notbii biimpri ndmfv con1i
      ) ABDEZFUNGHZIBJFZUNAKUPUOUPIZBDLZFZIZUOUTUQUSUPURJBURCJMNCOZPQZRQZUAZLZJ
      DVDCUBUCVCSFZVEJHCJCJVCSUDVFVAJFMVBRUEUFUGUHUIUJUKBDULTUMT $.
      $( [1-Jul-2013] $)
  $}

  $( The forward direction of ~ elee .  (Contributed by Scott Fenton,
     1-Jul-2013.) $)
  eleei $p |- ( A e. ( EE ` N ) -> A : ( 1 ... N ) --> RR ) $=
    ( cee cfv wcel c1 cfz co cr wf cn wb eleenn elee syl ibi ) ABCDEZFBGHIAJZQB
    KEQRLABMABNOP $.
    $( [1-Jul-2013] $)

  $( A point belongs to at most one Euclidean space.  (Contributed by Scott
     Fenton, 1-Jul-2013.) $)
  eedimeq $p |- ( ( A e. ( EE ` N ) /\ A e. ( EE ` M ) ) -> N = M ) $=
    ( cee cfv wcel wa c1 wceq cfz co cr wf eleei cdm sylan9req syl2an cn eleenn
    fdm cuz wb nnuz syl6eleq fzopth mpbid simprd ) ACDEFZABDEFZGZHHIZCBIZUJHCJK
    ZHBJKZIZUKULGZUHUMLAMZUNLAMZUOUIACNABNUQURUMAOUNUMLATUNLATPQUHCHUAEZFBRFUOU
    PUBUIUHCRUSACSUCUDABSRHBHCUEQUFUG $.
    $( [1-Jul-2013] $)

  ${
    $d N a n x y z i t $.  $d A x y z i t n $.  $d B x y z i t n $.
    $d C x y z i t n $.
    $( The binary relationship form of the betweenness predicate.  The
       statement ` A Btwn <. B , C >. ` should be informally read as " ` A `
       lies on a line segment between ` B ` and ` C ` .  This exact definition
       is abstracted away by Tarski's geometry axioms later on.  (Contributed
       by Scott Fenton, 3-Jun-2013.) $)
    brbtwn $p |- (
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
      ( A Btwn <. B , C >. <->
        E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... N )
           ( A ` i ) =
           ( ( ( 1 - t ) x. ( B ` i ) ) + ( t x. ( C ` i ) ) ) ) ) $=
      ( vn cv cfv wcel w3a c1 co cmul caddc wceq wral wrex wa cn vy vz vx cbtwn
      cop wbr cee cmin cfz cc0 cicc copab2 ccnv df-btwn breqi wb cvv opex mpan2
      brcnvg 3ad2ant1 df-br eleq1 3anbi1d fveq1 opreq2d opreq1d anbi12d rexbidv
      eqeq2d rexralbidv 3anbi2d 3anbi3d eqeq1d eloprabg wi simp1 eedimeq ancoms
      syl2an opreq2 raleqdv syl biimpd expimpd a1d rexlimdv eleenn fveq2 eleq2d
      3anbi123d rcla4ev exp32 mpcom impbid bitrd 3comr syl5bb ) BCDUEZUDUFBWSUA
      HZGHZUGIZJZUBHZXBJZUCHZXBJZKZEHZXFIZLAHZUHMZXIWTIZNMZXKXIXDIZNMZOMZPZELXA
      UIMZQAUJLUKMZRZSZGTRZUAUBUCULZUMZUFZBFUGIZJZCYGJZDYGJZKZXIBIZXLXICIZNMZXK
      XIDIZNMZOMZPZELFUIMZQZAXTRZBWSUDYEUAUCUBAEGUNUOYKYFWSBYDUFZUUAYHYIYFUUBUP
      ZYJYHWSUQJUUCCDURBWSYGUQYDUTUSVAUUBWSBUEYDJZYKUUAWSBYDVBYIYJYHUUDUUAUPYIY
      JYHKZUUDCXBJZDXBJZBXBJZKZYREXSQZAXTRZSZGTRZUUAYCUUFXEXGKZXJYNXPOMZPZEXSQA
      XTRZSZGTRUUFUUGXGKZXJYQPZEXSQAXTRZSZGTRUUMUAUBUCCDBYGYGYGWTCPZYBUURGTUVCX
      HUUNYAUUQUVCXCUUFXEXGWTCXBVCVDUVCXRUUPAEXTXSUVCXQUUOXJUVCXNYNXPOUVCXMYMXL
      NXIWTCVEVFVGVJVKVHVIXDDPZUURUVBGTUVDUUNUUSUUQUVAUVDXEUUGUUFXGXDDXBVCVLUVD
      UUPUUTAEXTXSUVDUUOYQXJUVDXPYPYNOUVDXOYOXKNXIXDDVEVFVFVJVKVHVIXFBPZUVBUULG
      TUVEUUSUUIUVAUUKUVEXGUUHUUFUUGXFBXBVCVMUVEUUTYRAEXTXSUVEXJYLYQXIXFBVEVNVK
      VHVIVOUUEUUMUUAUUEUULUUAGTUUEUULUUAVPXATJUUEUUIUUKUUAUUEUUISZUUKUUAUVFXAF
      PZUUKUUAUPUUEYIUUFUVGUUIYIYJYHVQUUFUUGUUHVQUUFYIUVGCFXAVRVSVTUVGUUJYTAXTU
      VGYREXSYSXAFLUIWAWBVIZWCWDWEWFWGFTJZUUEUUAUUMVPYIYJUVIYHCFWHVAUVIUUEUUAUU
      MUULUUEUUASGFTUVGUUIUUEUUKUUAUVGUUFYIUUGYJUUHYHUVGXBYGCXAFUGWIZWJUVGXBYGD
      UVJWJUVGXBYGBUVJWJWKUVHVHWLWMWNWOWPWQWRWPWR $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N n i x y $.  $d A n i x y $.  $d B n i x y $.  $d C n i x y $.
    $d D n i x y $.
    $( The binary relationship form of the congruence predicate.  The statement
       ` <. A , B >. Cgr <. C , D >. ` should be read informally as "the ` N `
       dimensional point ` A ` is as far from ` B ` as ` C ` is from ` D ` , or
       "the line segment ` A B ` is congruent to the line segment ` C D ` .
       This particular definition is encapsulated by Tarski's axioms later on.
       (Contributed by Scott Fenton, 3-Jun-2013.) $)
    brcgr $p |- ( ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
                    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. Cgr <. C , D >. <->
        sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) =
        sum_ i e. ( 1 ... N ) ( ( ( C ` i ) - ( D ` i ) ) ^ 2 ) ) ) $=
      ( vn cfv wcel wa co c1st c2nd cmin c2 cexp csu wceq cn fveq1d vx cop ccgr
      vy wbr cv cee cxp c1 cfz wrex opex eleq1 fveq2 opreq12d opreq1d sumeq2sdv
      anbi1d eqeq1d anbi12d rexbidv anbi2d eqeq2d brab opelxpg simplbda adantll
      df-cgr wb ad2ant2l simplrr eedimeq syl2anc adantlr opreq2 sumeq1d eqeq12d
      op1stg adantr op2ndg eqeqan12d ad2antrr bitrd biimpd expimpd rexlimdva wi
      syl eleenn opelxpi anim12i biimpar jca xpeq12 eleq2d rcla4ev sylan2 exp32
      ad2antll mpcom impbid syl5bb ) ABUBZCDUBZUCUEXCGUFZUGHZXFUHZIZXDXGIZJZUIX
      EUJKZEUFZXCLHZHZXLXCMHZHZNKZOPKZEQZXKXLXDLHZHZXLXDMHZHZNKZOPKZEQZRZJZGSUK
      ZAFUGHZIZBYJIZJZCYJIZDYJIZJZJZUIFUJKZXLAHZXLBHZNKZOPKZEQZYRXLCHZXLDHZNKZO
      PKZEQZRZUAUFZXGIZUDUFZXGIZJZXKXLUUJLHZHZXLUUJMHZHZNKZOPKZEQZXKXLUULLHZHZX
      LUULMHZHZNKZOPKZEQZRZJZGSUKXHUUMJZXSUVHRZJZGSUKYIUAUDXCXDUCABULCDULUUJXCR
      ZUVJUVMGSUVNUUNUVKUVIUVLUVNUUKXHUUMUUJXCXGUMURUVNUVAXSUVHUVNXKUUTXREUVNUU
      SXQOPUVNUUPXNUURXPNUVNXLUUOXMUUJXCLUNTUVNXLUUQXOUUJXCMUNTUOUPUQUSUTVAUULX
      DRZUVMYHGSUVOUVKXJUVLYGUVOUUMXIXHUULXDXGUMVBUVOUVHYFXSUVOXKUVGYEEUVOUVFYD
      OPUVOUVCYAUVEYCNUVOXLUVBXTUULXDLUNTUVOXLUVDYBUULXDMUNTUOUPUQVCUTVAUAUDEGV
      HVDYQYIUUIYQYHUUIGSYQXESIZJZXJYGUUIUVQXJJZYGUUIUVRYGYRXREQZYRYEEQZRZUUIUV
      RXEFRZYGUWAVIYQXJUWBUVPYQXJJDXFIZYOUWBYPXIUWCYMXHYOXIUWCYNYOXICXFIUWCCDXF
      XFYJVEVFVGVJYMYNYOXJVKDFXEVLVMVNUWBXSUVSYFUVTUWBXKYRXREXEFUIUJVOZVPUWBXKY
      RYEEUWDVPVQZWHYQUWAUUIVIUVPXJYMYPUVSUUCUVTUUHYMYRXRUUBEYMXQUUAOPYMXNYSXPY
      TNYKXNYSRYLYKXLXMAABYJVRTVSYMXLXOBABYJYJVTTUOUPUQYPYRYEUUGEYPYDUUFOPYPYAU
      UDYCUUENYPXLXTCYNXTCRYOCDYJVRVSTYPXLYBDCDYJYJVTTUOUPUQWAZWBWCWDWEWFFSIZYQ
      UUIYIWGYOUWGYMYNDFWIWSUWGYQUUIYIYQUUIJZUWGXCYJYJUHZIZXDUWIIZJZUWAJZYIUWHU
      WLUWAYQUWLUUIYMUWJYPUWKABYJYJWJCDYJYJWJWKVSYQUWAUUIUWFWLWMYHUWMGFSUWBXJUW
      LYGUWAUWBXHUWJXIUWKUWBXGUWIXCUWBXFYJRZUWNXGUWIRXEFUGUNZUWOXFYJXFYJWNVMZWO
      UWBXGUWIXDUWPWOUTUWEUTWPWQWRWTXAXB $.
      $( [3-Jun-2013] $)
  $}

  $( The function value of a point is a real.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveere $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. RR ) $=
    ( cee cfv wcel c1 cfz co cr wf eleei ffvelrn sylan ) ACDEFGCHIZJAKBOFBAEJFA
    CLOJBAMN $.
    $( [10-Jun-2013] $)

  $( The function value of a point is a complex.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveecn $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. CC ) $=
    ( cee cfv wcel c1 cfz co wa fveere recnd ) ACDEFBGCHIFJBAEABCKL $.
    $( [10-Jun-2013] $)

  ${
    $d A i $.  $d B i $.  $d N i $.
    $( Two points are equal iff they agree in all dimensions.  (Contributed by
       Scott Fenton, 10-Jun-2013.) $)
    eqeefv $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
       ( A = B <-> A. i e. ( 1 ... N ) ( A ` i ) = ( B ` i ) ) ) $=
      ( cee cfv wcel c1 cfz co wfn wceq cv wral wb cr wf eleei ffn syl eqfnfv2
      syl2an ) ADEFZGZAHDIJZKZBUEKZABLCMZAFUHBFLCUENOBUCGZUDUEPAQUFADRUEPASTUIU
      EPBQUGBDRUEPBSTCUEABUAUB $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    $( Two points are equal iff the square of the distance between them is
       zero.  (Contributed by Scott Fenton, 10-Jun-2013.)  (Revised by Mario
       Carneiro, 22-May-2014.) $)
    eqeelen $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        ( A = B <->
          sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) = 0 ) ) $=
      ( cee cfv wcel wa co cc0 wceq c1 wral cc wb cr fveere syl2anc recnd syl
      cv cmin c2 cexp cfz csu adantlr adantll sqeq0 subeq0 bitrd ralbidva fzfid
      resubcl resqcl cle wbr sqge0 fsum00 eqeefv 3bitr4rd ) ADEFZGZBVBGZHZCUAZA
      FZVFBFZUBIZUCUDIZJKZCLDUEIZMVGVHKZCVLMVLVJCUFJKABKVEVKVMCVLVEVFVLGZHZVKVI
      JKZVMVOVINGVKVPOVOVIVOVGPGZVHPGZVIPGZVCVNVQVDAVFDQUGZVDVNVRVCBVFDQUHZVGVH
      UNRZSVIUITVOVGNGVHNGVPVMOVOVGVTSVOVHWASVGVHUJRUKULVEVLVJCVELDUMVOVSVJPGWB
      VIUOTVOVSJVJUPUQWBVIURTUSABCDUTVA $.
      $( [22-May-2014] $) $( [10-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( Alternate characterization of betweenness, with no existential
       quantifiers.  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    brbtwn2 $p |-
        ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( A Btwn <. B , C >. <->
            ( A. i e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 /\
              A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) ) $=
      ( wcel wbr c1 cmin co cmul caddc wceq cc0 cle wa cc syl2anc opreq1d vk vt
      vp cfv cv wral wrex wi cr fveere 3ad2antl2 3ad2antl3 cneg resubcl 3adant3
      w3a syl opreq2d 1re 3ad2ant3 sylancr mul4 syl22anc recn 3ad2ant1 3ad2ant2
      recnd syl3anc ax-1cn subdir mp3an1 mulid2 3eqtr3d remulcl subsub4 mulneg1
      subdi simp1 3eqtrd eqtrd opreq12d subge0 mpbird mpbid eqbrtrd 3expa sylan
      mulge0 an32s ralrimiva fveecn anim12i anandirs subcl ancoms mulcom simp2r
      anandis simp2l simp3 mpan mulcl sub32 3eqtr3rd 3eqtr2d syl2an weq eqeq12d
      fveq2 rcla4cva opreq2 breq1d ralbidva opreqan12d anbi12d 3ad2antl1 anbi1d
      wb simpl2 eqeefv adantlr fveq1 orbi12d bitrd adantr 3adant1 opreq1 eqeq2d
      wo ex ralbidv rcla4ev wne cdiv breq12d simprr simprl clt necomd syl112anc
      cee cop cbtwn cfz cicc brbtwn jca c2 cexp sqval 0re elicc2i simp1bi negcl
      nncan simp2 subsub3 negeqd readdcl negsubdi2 mulneg2 resqcl simp2bi sqge0
      3eqtr2rd simp3bi le0neg2 3adantl1 simp1l ralrimivva opreqan12rd 2ralbidva
      simp1r biimprcd rexlimdva mulsuble0b simpl1 letri3 pm4.25 breq2d ad2antlr
      mulsub2 anbi2d syl5bb biimprd adantrd 0elunit 3jca addid1 ad2antll eqtr2d
      mul02 subid1i syl6eq exp32 syldd wn necon3abid df-ne rexbii bitri syl6bbr
      rexnal rcla4v ad2antrl simpl lesub1 ltlen imp simplrr mpbir2and divelunit
      letr posdif div2sub syl23anc lesub2 eqeltrd jaodan rcla42v simp11 simpl23
      syld simp12 simp13 simpl21 simpl12 simpl22 simpl3 subeq0 necon3bid divdir
      simpl13 npncan2 adddir simpl11 add32 addid2 addsub nnncan2 eqtr3d 3eqtr4d
      addsubass eqtr4d simpr adddi npcan addcl divmul div23 divsubdir syl331anc
      divid 3expia com23 ralrimdv anim12d syl6 rexlimdv sylbid pm2.61dne impbid
      ) AFUUAUDZGZBVVCGZCVVCGZUPZABCUUBUUCHUAUEZAUDZIUBUEZJKZVVHBUDZLKZVVJVVHCU
      DZLKZMKZNZUAIFUUDKZUFZUBOIUUEKZUGZDUEZBUDZVWBAUDZJKZVWBCUDZVWDJKZLKZOPHZD
      VVRUFZVWEEUEZCUDZVWKAUDZJKZLKZVWKBUDZVWMJKZVWGLKZNZEVVRUFDVVRUFZQZUBABCUA
      FUUFVVGVWAVXAVVGVVSVXAUBVVTVVGVVJVVTGZQZVWCVVKVWCLKZVVJVWFLKZMKZJKZVWFVXF
      JKZLKZOPHZDVVRUFZVXGVWLVVKVWPLKZVVJVWLLKZMKZJKZLKZVWPVXNJKZVXHLKZNZEVVRUF
      DVVRUFZVVSVXAUHVXCVXJDVVRVVGVWBVVRGZVXBVXJVVGVYAQZVWCUIGZVWFUIGZQVXBVXJVY
      BVYCVYDVVEVVDVYAVYCVVFBVWBFUJUKZVVFVVDVYAVYDVVECVWBFUJULZUUGVYCVYDVXBVXJV
      YCVYDVXBUPZVXIVVJVVKUMZLKZVWCVWFJKZUUHUUIKZLKZOPVYGVYLVYIVYJVYJLKZLKZVVJV
      YJLKZVYHVYJLKZLKZVXIVYGVYKVYMVYILVYGVYJRGZVYKVYMNVYGVYJVYCVYDVYJUIGZVXBVW
      CVWFUNUOZVGZVYJUUJUQURVYGVVJRGZVYRVYHRGZVYRVYQVYNNVXBVYCWUBVYDVXBVVJVXBVV
      JUIGZOVVJPHZVVJIPHZOIVVJUUKUSUULZUUMZVGZUTZWUAVYGVVKRGZWUCVYGVVKVXBVYCVVK
      UIGZVYDVXBIUIGZWUDWULUSWUHIVVJUNVAZUTZVGZVVKUUNUQWUAVVJVYJVYHVYJVBVCVYGVY
      OVXGVYPVXHLVYGVYOVVJVWCLKZVXEJKZVWCVXDJKZVXEJKZVXGVYGWUBVWCRGZVWFRGZVYOWU
      RNZWUJVYCVYDWVAVXBVWCVDVEZVYDVYCWVBVXBVWFVDVFZVVJVWCVWFVQZVHVYGWUQWUSVXEJ
      VYGIVVKJKZVWCLKZIVWCLKZVXDJKZWUQWUSVYGWUKWVAWVHWVJNZWUPWVDIRGZWUKWVAWVKVI
      IVVKVWCVJVKZSVYGWVGVVJVWCLVYGWVLWUBWVGVVJNZVIWUJIVVJUUOZVATVYGWVIVWCVXDJV
      YGWVAWVIVWCNZWVDVWCVLZUQTVMTVYGWVAVXDRGZVXERGZWUTVXGNZWVDVYGVXDVYGWULVYCV
      XDUIGZWUOVYCVYDVXBVRVVKVWCVNSZVGZVYGVXEVYGWUDVYDVXEUIGZVXBVYCWUDVYDWUHUTZ
      VYCVYDVXBUUPVVJVWFVNSZVGZVWCVXDVXEVOZVHVSVYGVYPVVKVYJLKZUMZVXFVWFJKZUMZVX
      HVYGWUKVYRVYPWWJNWUPWUAVVKVYJVPSVYGWWIWWKVYGWWIVXDVVKVWFLKZJKZVXDVWFVXEJK
      ZJKZWWKVYGWUKWVAWVBWWIWWNNWUPWVDWVEVVKVWCVWFVQVHVYGWWMWWOVXDJVYGWWMIVWFLK
      ZVXEJKZWWOVYGWUBWVBWWMWWRNZWUJWVEWVLWUBWVBWWSVIIVVJVWFVJVKZSVYGWWQVWFVXEJ
      VYGWVBWWQVWFNZWVEVWFVLZUQTVTURVYGWVRWVBWVSWWPWWKNWWCWVEWWGVXDVWFVXEUUQVHV
      SUURVYGVXFRGWVBWWLVXHNVYGVXFVYGWWAWWDVXFUIGWWBWWFVXDVXEUUSSVGWVEVXFVWFUUT
      SVSWAUVEVYGVYLVVJVVKLKZVYKLKZUMZOPVYGVYLWXCUMZVYKLKZWXEVYGVYIWXFVYKLVYGWU
      BWUKVYIWXFNWUJWUPVVJVVKUVASTVYGWXCRGVYKRGWXGWXENVYGWXCVYGWUDWULWXCUIGZWWE
      WUOVVJVVKVNSZVGVYGVYKVYGVYSVYKUIGZVYTVYJUVBUQZVGWXCVYKVPSVTVYGOWXDPHZWXEO
      PHZVYGWXHOWXCPHZWXJOVYKPHZWXLWXIVXBVYCWXNVYDVXBWUDWUEWULOVVKPHZWXNWUHVXBW
      UDWUEWUFWUGUVCWUNVXBWXPWUFVXBWUDWUEWUFWUGUVFVXBWUMWUDWXPWUFXRUSWUHIVVJWBV
      AWCVVJVVKWHVCUTWXKVYGVYSWXOVYTVYJUVDUQWXCVYKWHVCVYGWXDUIGZWXLWXMXRVYGWXHW
      XJWXQWXIWXKWXCVYKVNSWXDUVGUQWDWEWEWFWGWIWJVXCVXSDEVVRVVRVVGVYAVWKVVRGZQZV
      XBVXSVVGWXSQWVAWVBQZVWPRGZVWLRGZQZQZWUBVXSVXBVVEVVFWXSWYDVVDVVEVVFQZVYAWX
      RWYDWYEVYAQWXTWYEWXRQWYCVVEVVFVYAWXTVVEVYAQWVAVVFVYAQWVBBVWBFWKCVWBFWKWLW
      MVVEVVFWXRWYCVVEWXRQWYAVVFWXRQWYBBVWKFWKCVWKFWKWLWMWLWRUVHWUIWXTWYCWUBVXS
      WXTWYCWUBUPZWXCVYJVWLVWPJKZLKZLKZWXCVWPVWLJKZVWFVWCJKZLKZLKZVXPVXRWYFWYHW
      YLWXCLWYFWYHWYGVYJLKZWYLWYFVYRWYGRGZWYHWYNNWXTWYCVYRWUBVWCVWFWNVEZWYCWXTW
      YOWUBWYBWYAWYOVWLVWPWNWOVFZVYJWYGWPSWYFWYBWYAWVAWVBWYNWYLNWXTWYAWYBWUBWQZ
      WXTWYAWYBWUBWSZWVAWVBWYCWUBUVIZWVAWVBWYCWUBUVMZVWLVWPVWCVWFUWBVCVTURWYFWY
      IVYOVVKWYGLKZLKZVXPWYFWUBWUKVYRWYOWYIXUCNWXTWYCWUBWTZWUBWXTWUKWYCWVLWUBWU
      KVIIVVJWNXAUTZWYPWYQVVJVVKVYJWYGVBVCWYFVYOVXGXUBVXOLWYFVYOWURWUTVXGWYFWUB
      WVAWVBWVCXUDWYTXUAWVFVHWYFWUQWUSVXEJWYFWVHWVJWUQWUSWYFWUKWVAWVKXUEWYTWVMS
      WYFWVGVVJVWCLWUBWXTWVNWYCWVLWUBWVNVIWVOXAUTZTWYFWVIVWCVXDJWYFWVAWVPWYTWVQ
      UQTVMTWYFWVAWVRWVSWVTWYTWYFWUKWVAWVRXUEWYTVVKVWCXBSZWYFWUBWVBWVSXUDXUAVVJ
      VWFXBSZWWHVHVSWYFXUBVWLVXMJKZVXLJKZVWLVXLJKVXMJKZVXOWYFXUBVVKVWLLKZVXLJKZ
      XUJWYFWUKWYBWYAXUBXUMNXUEWYRWYSVVKVWLVWPVQVHWYFXULXUIVXLJWYFXULIVWLLKZVXM
      JKZXUIWYFWUBWYBXULXUONZXUDWYRWVLWUBWYBXUPVIIVVJVWLVJVKSWYFXUNVWLVXMJWYFWY
      BXUNVWLNWYRVWLVLUQTVTTVTWYFWYBVXMRGZVXLRGZXUJXUKNWYRWYFWUBWYBXUQXUDWYRVVJ
      VWLXBSZWYFWUKWYAXURXUEWYSVVKVWPXBSZVWLVXMVXLXCVHWYFWYBXURXUQXUKVXONWYRXUT
      XUSVWLVXLVXMVOVHVSWAVTWYFWYMVVJWYJLKZVVKWYKLKZLKZVXRWYFWUBWUKWYJRGZWYKRGZ
      WYMXVCNXUDXUEWYCWXTXVDWUBVWPVWLWNVFWXTWYCXVEWUBWVBWVAXVEVWFVWCWNWOVEVVJVV
      KWYJWYKVBVCWYFXVAVXQXVBVXHLWYFXVAVVJVWPLKZVXMJKZVWPVXLJKZVXMJKZVXQWYFWUBW
      YAWYBXVAXVGNXUDWYSWYRVVJVWPVWLVQVHWYFXVHXVFVXMJWYFWVGVWPLKZIVWPLKZVXLJKZX
      VFXVHWYFWUKWYAXVJXVLNZXUEWYSWVLWUKWYAXVMVIIVVKVWPVJVKSWYFWVGVVJVWPLXUFTWY
      FXVKVWPVXLJWYFWYAXVKVWPNWYSVWPVLUQTXDTWYFWYAXURXUQXVIVXQNWYSXUTXUSVWPVXLV
      XMVOVHXEWYFXVBWWMVXDJKZWWOVXDJKZVXHWYFWUKWVBWVAXVBXVNNXUEXUAWYTVVKVWFVWCV
      QVHWYFWWMWWOVXDJWYFWWMWWRWWOWYFWUBWVBWWSXUDXUAWWTSWYFWWQVWFVXEJWYFWVBWXAX
      UAWXBUQTVTTWYFXVOVWFVXDJKVXEJKZVXHWYFWVBWVSWVRXVOXVPNXUAXUHXUGVWFVXEVXDXC
      VHWYFWVBWVRWVSXVPVXHNXUAXUGXUHVWFVXDVXEVOVHVTVSWAVTVMWFXFWIUVJVVSVXAVXKVX
      TQVVSVWJVXKVWTVXTVVSVWIVXJDVVRVVSVYAQZVWDVXFNZVWIVXJXRVVQXVRUAVWBVVRUADXG
      ZVVIVWDVVPVXFVVHVWBAXIXVSVVMVXDVVOVXEMXVSVVLVWCVVKLVVHVWBBXIURXVSVVNVWFVV
      JLVVHVWBCXIURWAXHXJZXVRVWHVXIOPXVRVWEVXGVWGVXHLVWDVXFVWCJXKZVWDVXFVWFJXKZ
      WAXLUQXMVVSVWSVXSDEVVRVVRVVSVYAWXRVWSVXSXRZXVQXVRVWMVXNNZXWCVVSWXRQXVTVVQ
      XWDUAVWKVVRUAEXGZVVIVWMVVPVXNVVHVWKAXIXWEVVMVXLVVOVXMMXWEVVLVWPVVKLVVHVWK
      BXIURXWEVVNVWLVVJLVVHVWKCXIURWAXHXJXVRXWDQVWOVXPVWRVXRXVRXWDVWEVXGVWNVXOL
      XWAVWMVXNVWLJXKXNXWDXVRVWQVXQVWGVXHLVWMVXNVWPJXKXWBUVKXHXFWRUVLXOUVNSUVOV
      VGVXAVWCVWDPHZVWDVWFPHZQZVWFVWDPHZVWDVWCPHZQZYIZDVVRUFZVWTQZVWAVVGVWJXWMV
      WTVVGVWIXWLDVVRVYBVYCVWDUIGZVYDVWIXWLXRVYEVVDVVEVYAXWOVVFAVWBFUJXPZVYFVWC
      VWDVWFUVPVHXMXQVVGXWNVWAUHZBCVVGBCNZXWNBANZVWAVVGXWRXWNXWSUHVVGXWRQZXWMXW
      SVWTXWTXWSXWMXWTXWSVWCVWDNZDVVRUFZXWMXWTVVEVVDXWSXXBXRVVDVVEVVFXWRXSVVDVV
      EVVFXWRUVQBADFXTSXWTXXAXWLDVVRXWTVYAQZXXAXWFXWJQZXWLXXCVYCXWOXXAXXDXRVVGV
      YAVYCXWRVYEYAVVGVYAXWOXWRXWPYAVWCVWDUVRSXXDXXDXXDYIZXXCXWLXXDUVSXWRXXEXWL
      XRVVGVYAXWRXXDXWHXXDXWKXWRXWJXWGXWFXWRVWCVWFVWDPVWBBCYBZUVTUWCXWRXWFXWIXW
      JXWRVWCVWFVWDPXXFXLXQYCUWAUWDYDXMYDUWEUWFYJVVGXWRXWSVWAVVGXWRXWSQZQZOVVTG
      VVIIVVLLKZOVVNLKZMKZNZUAVVRUFZVWAUWGXXHXXLUAVVRVVGVVHVVRGZXXGXXLVVGXXNQZV
      VIRGZVVLRGZVVNRGZUPZXXGXXLXXOXXPXXQXXRVVDVVEXXNXXPVVFAVVHFWKZXPVVEVVDXXNX
      XQVVFBVVHFWKZUKVVFVVDXXNXXRVVECVVHFWKZULUWHXXSXXGQXXKVVLVVIXXSXXKVVLNZXXG
      XXQXXRXYCXXPXXQXXRQXXKVVLOMKZVVLXXQXXRXXIVVLXXJOMVVLVLVVNUWLZXNXXQXYDVVLN
      XXRVVLUWIYEVTYFYEXWSVVLVVINXXSXWRVVHBAYBUWJUWKWGWIWJVVSXXMUBOVVTVVJONZVVQ
      XXLUAVVRXYFVVPXXKVVIXYFVVMXXIVVOXXJMXYFVVKIVVLLXYFVVKIOJKIVVJOIJXKIVIUWMU
      WNTVVJOVVNLYGWAYHYKYLVAUWOUWPVVGBCYMZUCUEZBUDZXYHCUDZYMZUCVVRUGZXWQVVGXYG
      XYIXYJNZUCVVRUFZUWQZXYLVVGXYNBCVVEVVFXWRXYNXRVVDBCUCFXTYFUWRXYLXYMUWQZUCV
      VRUGXYOXYKXYPUCVVRXYIXYJUWSUWTXYMUCVVRUXCUXAUXBVVGXYKXWQUCVVRVVGXYHVVRGZX
      YKXWQVVGXYQXYKQZQZXWNXYHAUDZXYIJKZXYJXYIJKZYNKZVVTGZVVIIYUCJKZVVLLKZYUCVV
      NLKZMKZNZUAVVRUFZQVWAXYSXWMYUDVWTYUJXYSXWMXYIXYTPHZXYTXYJPHZQZXYJXYTPHZXY
      TXYIPHZQZYIZYUDXYQXWMYUQUHVVGXYKXWLYUQDXYHVVRDUCXGZXWHYUMXWKYUPYURXWFYUKX
      WGYULYURVWCXYIVWDXYTPVWBXYHBXIZVWBXYHAXIZYOYURVWDXYTVWFXYJPYUTVWBXYHCXIZY
      OXOYURXWIYUNXWJYUOYURVWFXYJVWDXYTPYVAYUTYOYURVWDXYTVWCXYIPYUTYUSYOXOYCUXD
      UXEXYSYUQYUDXYSYUMYUDYUPXYSYUMQZYUDYUAYUBPHZYVBYULYVCXYSYUKYULYPXYSYULYVC
      XRZYUMXYSXYTUIGZXYJUIGZXYIUIGZYVDVVGVVDXYQYVEXYRVVDVVEVVFVRXYQXYKUXFZAXYH
      FUJXFZVVGVVFXYQYVFXYRVVDVVEVVFWTYVHCXYHFUJXFZXYSVVEXYQYVGVVDVVEVVFXYRXSVV
      GXYQXYKYQBXYHFUJSZXYTXYJXYIUXGVHYEWDYVBYUAUIGZOYUAPHZYUBUIGZOYUBYRHZYUDYV
      CXRXYSYVLYUMXYSYVEYVGYVLYVIYVKXYTXYIUNSYEYVBYVMYUKXYSYUKYULYQXYSYVMYUKXRZ
      YUMXYSYVEYVGYVPYVIYVKXYTXYIWBSYEWCXYSYVNYUMXYSYVFYVGYVNYVJYVKXYJXYIUNSYEY
      VBXYIXYJYRHZYVOYVBYVQXYIXYJPHZXYJXYIYMZXYSYVQYVRYVSQXRZYUMXYSYVGYVFYVTYVK
      YVJXYIXYJUXHSYEXYSYUMYVRXYSYVGYVEYVFYUMYVRUHYVKYVIYVJXYIXYTXYJUXMVHUXIYVB
      XYIXYJVVGXYQXYKYUMUXJYSUXKXYSYVQYVOXRZYUMXYSYVGYVFYWAYVKYVJXYIXYJUXNSYEWD
      YUAYUBUXLVCWCXYSYUPQZYUCXYIXYTJKZXYIXYJJKZYNKZVVTXYSYUCYWENZYUPXYSXYTRGZX
      YIRGZXYJRGZYWHYVSYWFXYSXYTYVIVGZXYSXYIYVKVGZXYSXYJYVJVGZYWKXYSXYIXYJVVGXY
      QXYKYPYSXYTXYIXYJXYIUXOUXPYEYWBYWEVVTGZYWCYWDPHZYWBYUNYWNXYSYUNYUOYQXYSYU
      NYWNXRZYUPXYSYVFYVEYVGYWOYVJYVIYVKXYJXYTXYIUXQVHYEWDYWBYWCUIGZOYWCPHZYWDU
      IGZOYWDYRHZYWMYWNXRXYSYWPYUPXYSYVGYVEYWPYVKYVIXYIXYTUNSYEYWBYWQYUOXYSYUNY
      UOYPXYSYWQYUOXRZYUPXYSYVGYVEYWTYVKYVIXYIXYTWBSYEWCXYSYWRYUPXYSYVGYVFYWRYV
      KYVJXYIXYJUNSYEYWBXYJXYIYRHZYWSYWBYXAXYJXYIPHZXYKXYSYXAYXBXYKQXRZYUPXYSYV
      FYVGYXCYVJYVKXYJXYIUXHSYEXYSYUPYXBXYSYVFYVEYVGYUPYXBUHYVJYVIYVKXYJXYTXYIU
      XMVHUXIVVGXYQXYKYUPUXJUXKXYSYXAYWSXRZYUPXYSYVFYVGYXDYVJYVKXYJXYIUXNSYEWDY
      WCYWDUXLVCWCUXRUXSYJUYCXYSVWTYUIUAVVRXYSXXNVWTYUIVVGXYRXXNVWTYUIUHVVGXYRX
      XNUPZVWTYWCVVNVVIJKZLKZVVLVVIJKZXYJXYTJKZLKZNZYUIYXEXYQXXNVWTYXKUHVVGXYQX
      YKXXNWSVVGXYRXXNWTZVWSYXKYWCVWNLKZVWQYXILKZNDEXYHVVHVVRVVRYURVWOYXMVWRYXN
      YURVWEYWCVWNLYURVWCXYIVWDXYTJYUSYUTWATYURVWGYXIVWQLYURVWFXYJVWDXYTJYVAYUT
      WAURXHEUAXGZYXMYXGYXNYXJYXOVWNYXFYWCLYXOVWLVVNVWMVVIJVWKVVHCXIVWKVVHAXIZW
      AURYXOVWQYXHYXILYXOVWPVVLVWMVVIJVWKVVHBXIYXPWATXHUXTSYXEXXPXXQXXRYWGYWHYW
      IYVSYXKYUIUHYXEVVDXXNXXPVVDVVEVVFXYRXXNUYAYXLXXTSYXEVVEXXNXXQVVDVVEVVFXYR
      XXNUYDYXLXYASYXEVVFXXNXXRVVDVVEVVFXYRXXNUYEYXLXYBSVVGXYRYWGXXNYWJUOVVGXYR
      YWHXXNYWKUOVVGXYRYWIXXNYWLUOYXEXYIXYJVVGXYQXYKXXNWQYSXXSYWGYWHYWIUPZYVSUP
      ZYXKYUIYXRYXKQZYXIVVLLKZYUAVVNLKZMKZYUBYNKZYXTYUBYNKZYYAYUBYNKZMKZVVIYUHY
      XSYXTRGZYYARGZYUBRGZYUBOYMZYYCYYFNYXSYXIRGZXXQYYGYXSYWIYWGYYKYWGYWHYWIXXS
      YVSYXKUYBZYWGYWHYWIXXSYVSYXKUYFZXYJXYTWNSZXXPXXQXXRYXQYVSYXKUYGZYXIVVLXBS
      ZYXSYUARGZXXRYYHYXSYWGYWHYYQYYMYWGYWHYWIXXSYVSYXKUYHZXYTXYIWNSZXXPXXQXXRY
      XQYVSYXKUYMZYUAVVNXBSZYXSYWIYWHYYIYYLYYRXYJXYIWNSZYXSYYJYVSXXSYXQYVSYXKUY
      IYXSYUBOXYJXYIYXSYWIYWHYUBONXYJXYINXRYYLYYRXYJXYIUYJSUYKWCZYXTYYAYUBUYLYT
      YXSYYCVVINZYUBVVILKZYYBNZYXSUUUEYXGYXIVVILKZMKZYYAMKZYXJUUUGMKZYYAMKYYBYX
      SUUUEYWCVVNLKZUUUEMKZYYAMKZUUUIYXSUUUKYYAMKZUUUEMKZOUUUEMKZUUUMUUUEYXSUUU
      NOUUUEMYXSYWCYUAMKZVVNLKZXXJUUUNOYXSUUUQOVVNLYXSYWHYWGUUUQONYYRYYMXYIXYTU
      YNSTYXSYWCRGZYYQXXRUUURUUUNNYXSYWHYWGUUUSYYRYYMXYIXYTWNSZYYSYYTYWCYUAVVNU
      YOVHYXSXXRXXJONYYTXYEUQVMTYXSUUUKRGZYYHUUUERGZUUUOUUUMNYXSUUUSXXRUUVAUUUT
      YYTYWCVVNXBSZUUUAYXSYYIXXPUUVBUUUBXXPXXQXXRYXQYVSYXKUYPZYUBVVIXBSZUUUKYYA
      UUUEUYQVHYXSUUVBUUUPUUUENUUVEUUUEUYRUQXDYXSUUULUUUHYYAMYXSUUUKUUUGMKYWCVV
      ILKZJKZUUUKUUVFJKZUUUGMKZUUULUUUHYXSUUVAUUUGRGZUUVFRGZUUVGUUVINUUVCYXSYYK
      XXPUUVJYYNUUVDYXIVVIXBSZYXSUUUSXXPUUVKUUUTUUVDYWCVVIXBSZUUUKUUUGUUVFUYSVH
      YXSUUULUUUKUUUGUUVFJKZMKZUUVGYXSUUUEUUVNUUUKMYXSYXIYWCJKZVVILKZUUUEUUVNYX
      SUUVPYUBVVILYXSYWIYWHYWGUUVPYUBNYYLYYRYYMXYJXYIXYTUYTVHTYXSYYKUUUSXXPUUVQ
      UUVNNYYNUUUTUUVDYXIYWCVVIVJVHVUAURYXSUUVAUUVJUUVKUUVGUUVONUUVCUUVLUUVMUUU
      KUUUGUUVFVUCVHVUDYXSYXGUUVHUUUGMYXSUUUSXXRXXPYXGUUVHNUUUTYYTUUVDYWCVVNVVI
      VQVHTVUBTVTYXSUUUHUUUJYYAMYXSYXGYXJUUUGMYXRYXKVUETTYXSUUUJYXTYYAMYXSUUUJY
      XIYXHLKZUUUGMKZYXIYXHVVIMKZLKZYXTYXSYXJUUVRUUUGMYXSYXHRGZYYKYXJUUVRNYXSXX
      QXXPUUWBYYOUUVDVVLVVIWNSZYYNYXHYXIWPSTYXSYYKUUWBXXPUUWAUUVSNYYNUUWCUUVDYX
      IYXHVVIVUFVHYXSUUVTVVLYXILYXSXXQXXPUUVTVVLNYYOUUVDVVLVVIVUGSURXETVSYXSYYB
      RGZXXPYYIYYJUUUDUUUFXRYXSYYGYYHUUWDYYPUUUAYXTYYAVUHSUUVDUUUBUUUCYYBVVIYUB
      VUIYTWCYXSYYDYUFYYEYUGMYXSYYDYXIYUBYNKZVVLLKZYUFYXSYYKXXQYYIYYJYYDUUWFNYY
      NYYOUUUBUUUCYXIVVLYUBVUJYTYXSUUWEYUEVVLLYXSYUBYUAJKZYUBYNKZYUBYUBYNKZYUCJ
      KZUUWEYUEYXSYYIYYQYYIYYJUUWHUUWJNUUUBYYSUUUBUUUCYUBYUAYUBVUKYTYXSUUWGYXIY
      UBYNYXSYWIYWGYWHUUWGYXINYYLYYMYYRXYJXYTXYIUYTVHTYXSUUWIIYUCJYXSYYIYYJUUWI
      INUUUBUUUCYUBVUMSTVMTVTYXSYYQXXRYYIYYJYYEYUGNYYSYYTUUUBUUUCYUAVVNYUBVUJYT
      WAVMYJVULUYCVUNVUOVUPVUQVVSYUJUBYUCVVTVVJYUCNZVVQYUIUAVVRUUWKVVPYUHVVIUUW
      KVVMYUFVVOYUGMUUWKVVKYUEVVLLVVJYUCIJXKTVVJYUCVVNLYGWAYHYKYLVURUWOVUSVUTVV
      AVUTVVBYD $.
      $( [24-Jun-2013] $)
  $}


  $( Lemma for ~ colinearalg .  Expand out a multiplication. $)
  colinearalglem1 $p |- (
    ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
      ( D e. CC /\ E e. CC /\ F e. CC ) ) ->
    ( ( ( B - A ) x. ( F - D ) ) = ( ( E - D ) x. ( C - A ) ) <->
      ( ( B x. F ) - ( ( A x. F ) + ( B x. D ) ) ) =
      ( ( C x. E ) - ( ( A x. E ) + ( C x. D ) ) ) ) ) $=
    ( cc wcel cmin co cmul wceq caddc subcl syl2anc syl3anc mulcl syl2an subdir
    w3a wa simpl2 simpl1 simpr3 simpr1 subdi simp2 simp3 simp1 opreq12d subsub3
    addsub eqtr2d subsub4 opreq1d 3eqtr3d eqtrd simpr2 simpl3 mulcom eqeq12d wb
    3eqtrd addcl addcan2 bitrd ) AGHZBGHZCGHZTZDGHZEGHZFGHZTZUAZBAIJZFDIJKJZEDI
    JZCAIJZKJZLBFKJZAFKJZBDKJZMJZIJZADKJZMJZCEKJZAEKJZCDKJZMJZIJZWFMJZLZWEWLLZV
    OVQWGVTWMVOVQVPFKJZVPDKJZIJZWGVOVPGHZVMVKVQWRLVOVHVGWSVGVHVIVNUBZVGVHVIVNUC
    ZBANOVJVKVLVMUDZVJVKVLVMUEZVPFDUFPVOWAWBIJZWFMJWCIJZXDWCIJZWFMJZWRWGVOXDGHZ
    WFGHZWCGHZXEXGLVOWAGHZWBGHZXHVJVHVMXKVNVGVHVIUGZVKVLVMUHZBFQRZVJVGVMXLVNVGV
    HVIUIZXNAFQRZWAWBNOZVJVGVKXIVNXPVKVLVMUIZADQRZVJVHVKXJVNXMXSBDQRZXDWFWCULPV
    OWRXDWCWFIJZIJZXEVOWPXDWQYBIVOVHVGVMWPXDLWTXAXBBAFSPVOVHVGVKWQYBLWTXAXCBADS
    PUJVOXHXJXIYCXELXRYAXTXDWCWFUKPUMVOXFWEWFMVOXKXLXJXFWELXOXQYAWAWBWCUNPUOUPU
    QVOVTVSVRKJZVSEKJZVSDKJZIJZWMVOVRGHZVSGHZVTYDLVOVLVKYHVJVKVLVMURZXCEDNOVOVI
    VGYIVGVHVIVNUSZXACANOZVRVSUTOVOYIVLVKYDYGLYLYJXCVSEDUFPVOWHWIIJZWFMJWJIJZYM
    WJIJZWFMJZYGWMVOYMGHZXIWJGHZYNYPLVOWHGHZWIGHZYQVJVIVLYSVNVGVHVIUHZVKVLVMUGZ
    CEQRZVJVGVLYTVNXPUUBAEQRZWHWINOZXTVJVIVKYRVNUUAXSCDQRZYMWFWJULPVOYGYMWJWFIJ
    ZIJZYNVOYEYMYFUUGIVOVIVGVLYEYMLYKXAYJCAESPVOVIVGVKYFUUGLYKXAXCCADSPUJVOYQYR
    XIUUHYNLUUEUUFXTYMWJWFUKPUMVOYOWLWFMVOYSYTYRYOWLLUUCUUDUUFWHWIWJUNPUOUPVCVA
    VOWEGHZWLGHZXIWNWOVBVOXKWDGHZUUIXOVOXLXJUUKXQYAWBWCVDOWAWDNOVOYSWKGHZUUJUUC
    VOYTYRUULUUDUUFWIWJVDOWHWKNOXTWEWLWFVEPVF $.
    $( [24-Jun-2013] $)

  ${
    $d A i j $.  $d B i j $.  $d C i j $.  $d N i j $.
    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem2 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( C ` i ) - ( B ` i ) ) x. ( ( A ` j ) - ( B ` j ) ) ) =
         ( ( ( C ` j ) - ( B ` j ) ) x. ( ( A ` i ) - ( B ` i ) ) ) ) ) $=
      ( cfv wcel cmin co cmul wceq cc fveecn syl2an caddc mulcl syl2anc syl3anc
      w3a cee cv c1 cfz wa simp1 simpl simp2 simp3 simpr subsub4 subsub3 eqcomd
      wb addcl opreq2d subcl subsub eqtrd 3eqtr2d addsub12 3eqtrd subadd2 eqcom
      eqeq1d syl6bb addsub addcom eqtr3d eqeq2d subeqrev syl22anc 3bitrd subadd
      opreq1d syl5rbbr 3bitr3d colinearalglem1 3anrot syl2anb 3bitr4d 2ralbidva
      sub32 bitrd syl33anc ) AFUAGZHZBWFHZCWFHZTZDUBZBGZWKAGZIJEUBZCGZWNAGZIJKJ
      WNBGZWPIJWKCGZWMIJKJLZWRWLIJWPWQIJKJWOWQIJWMWLIJKJLZDEUCFUDJZXAWJWKXAHZWN
      XAHZUEZUEWMMHZWLMHZWRMHZWPMHZWQMHZWOMHZWSWTUNWJWGXBXEXDWGWHWIUFZXBXCUGZAW
      KFNOWJWHXBXFXDWGWHWIUHZXLBWKFNOWJWIXBXGXDWGWHWIUIZXLCWKFNOWJWGXCXHXDXKXBX
      CUJZAWNFNOWJWHXCXIXDXMXOBWNFNOWJWIXCXJXDXNXOCWNFNOXEXFXGTZXHXIXJTZUEZWLWO
      KJZWMWOKJZWLWPKJZPJZIJWRWQKJZWMWQKJZWRWPKJZPJZIJLZYEYAYCPJIJZXTXSYDPJZIJZ
      LZWSWTXRYBXSIJZYDIJZYEYCIJZLZYAYJPJZYNLZYGYKXRYMYPYNXRYMXTXSIJZYAYDIJZPJZ
      YAYRYDIJZPJZYPXRYMYBYIIJZXTYIYAIJZIJZYTXRYBMHZXSMHZYDMHZYMUUCLXRXTMHZYAMH
      ZUUFXPXEXJUUIXQXEXFXGUFZXHXIXJUIZWMWOQOZXPXFXHUUJXQXEXFXGUHZXHXIXJUFZWLWP
      QOZXTYAUORZXPXFXJUUGXQUUNUULWLWOQOZXPXEXIUUHXQUUKXHXIXJUHZWMWQQOZYBXSYDUK
      SXRUUIYIMHZUUJUUEUUCLUUMXRUUGUUHUVAUURUUTXSYDUORZUUPXTYIYAULSXRUUEXTXSYSI
      JZIJZYTXRUUDUVCXTIXRUVCUUDXRUUGUUJUUHUVCUUDLUURUUPUUTXSYAYDULSUMUPXRUUIUU
      GYSMHZUVDYTLUUMUURXRUUJUUHUVEUUPUUTYAYDUQRXTXSYSURSUSUTXRYRMHZUUJUUHYTUUB
      LXRUUIUUGUVFUUMUURXTXSUQRUUPUUTYRYAYDVASXRUUAYJYAPXRUUIUUGUUHUUAYJLUUMUUR
      UUTXTXSYDUKSUPVBVEXRYOYLYNYDPJZLZYLYFYCIJZLZYGXRYOUVGYLLZUVHXRYLMHZUUHYNM
      HZYOUVKUNXRUUFUUGUVLUUQUURYBXSUQRUUTXRYEMHZYCMHZUVMXPXGXHUVNXQXEXFXGUIZUU
      OWRWPQOZXPXGXIUVOXQUVPUUSWRWQQOZYEYCUQRZYLYDYNVCSUVGYLVDVFXRUVGUVIYLXRYEY
      DPJZYCIJZUVGUVIXRUVNUUHUVOUWAUVGLUVQUUTUVRYEYDYCVGSXRUVTYFYCIXRUVNUUHUVTY
      FLUVQUUTYEYDVHRVOVIVJXRUUFUUGYFMHZUVOUVJYGUNUUQUURXRUUHUVNUWBUUTUVQYDYEUO
      RUVRYBXSYFYCVKVLVMXRYQYJYHLZYKXRYQYJYNYAIJZLZUWCUWEUWDYJLZXRYQUWDYJVDXRUV
      MUUJYJMHZUWFYQUNUVSUUPXRUUIUVAUWGUUMUVBXTYIUQRYNYAYJVNSVPXRUWDYHYJXRUWDYE
      YAIJYCIJZYHXRUVNUVOUUJUWDUWHLUVQUVRUUPYEYCYAWCSXRUVNUUJUVOUWHYHLUVQUUPUVR
      YEYAYCUKSUSVJWDYJYHVDVFVQWMWLWRWPWQWOVRXPXFXGXETXIXJXHTWTYKUNXQXEXFXGVSXH
      XIXJVSWLWRWMWQWOWPVRVTWAWEWB $.
      $( [24-Jun-2013] $)

    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem3 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( A ` i ) - ( C ` i ) ) x. ( ( B ` j ) - ( C ` j ) ) ) =
         ( ( ( A ` j ) - ( C ` j ) ) x. ( ( B ` i ) - ( C ` i ) ) ) ) ) $=
      ( cee cfv wcel w3a cv cmin co cmul wceq c1 cfz wral colinearalglem2 wb
      3comr bitrd ) AFGHZIZBUCIZCUCIZJDKZBHZUGAHZLMEKZCHZUJAHZLMNMUJBHZULLMUGCH
      ZUILMNMOEPFQMZRDUORUNUHLMULUMLMNMUKUMLMUIUHLMNMOEUORDUORZUIUNLMUMUKLMNMUL
      UKLMUHUNLMNMOEUORDUORZABCDEFSUEUFUDUPUQTBCADEFSUAUB $.
      $( [24-Jun-2013] $)


  $}

  ${
    $d A i $.  $d C i $.  $d K i $.  $d N i $.
    $( Lemma for ~ colinearalg .  Prove a disjunction that will be needed in
       the final proof. $)
    colinearalglem4 $p |- (
       ( ( A e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\ K e. RR ) ->
       ( A. i e. ( 1 ... N ) ( ( ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) +
         ( A ` i ) ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( C ` i ) - ( ( K x. ( ( C ` i ) -
         ( A ` i ) ) ) + ( A ` i ) ) ) x. ( ( A ` i ) -
         ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( A ` i ) - ( C ` i ) ) x. ( ( ( K x.
         ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) - ( C ` i ) ) ) <_ 0 ) ) $=
      ( wcel wa cr cc0 cle wbr c1 cmin co cmul cc wceq recnd syl2anc syl cee cv
      cfv w3o cfz wral relin01 adantl fveere adantlr adantll jca c2 cexp simprl
      caddc resubcl ancoms adantr mulass syl3anc remulcl ad2antrr pncan opreq1d
      recn sqval opreq2d 3eqtr4d wo simprr sqge0 orcd wb resqcl mulle0b eqbrtrd
      mpbird sylan an32s ralrimiva expr cneg ax-1cn subdir mp3an1 mulid2 eqtr2d
      ad2antlr sub32 subsub4 3eqtr3rd df-neg syl6eqr opreq12d 1re mpan ad2antrl
      subid mulneg2 syl22anc negeqd 3eqtrd simpl sylancr subge0 biimpar adantrl
      mul4 mulge0 breqtrd le0neg2 mpbid negsubdi2 simplr simpll peano2rem mul12
      mulneg1 eqtr4d eqtrd mp3an2 subsub3 mpan2 3orim123d mpd ) AEUAUCZFZBYGFZG
      ZDHFZGZDIJKZIDJKZDLJKZGZLDJKZUDZDCUBZBUCZYSAUCZMNZONZUUAUPNZUUAMNZUUBONZI
      JKZCLEUENZUFZYTUUDMNZUUAUUDMNZONZIJKZCUUHUFZUUAYTMNZUUDYTMNZONZIJKZCUUHUF
      ZUDYKYRYJDUGUHYLYMUUIYPUUNYQUUSYJYKYMUUIYJYKYMGZGUUGCUUHYJYSUUHFZUUTUUGYJ
      UVAGZUUAHFZYTHFZGZUUTUUGUVBUVCUVDYHUVAUVCYIAYSEUIUJYIUVAUVDYHBYSEUIUKULZU
      VEUUTGZUUFDUUBUMUNNZONZIJUVGUUCUUBONZDUUBUUBONZONZUUFUVIUVGDPFZUUBPFZUVNU
      VJUVLQUVGDUVEYKYMUOZRUVGUUBUVEUUBHFZUUTUVDUVCUVPYTUUAUQZURZUSZRZUVTDUUBUU
      BUTVAUVGUUEUUCUUBOUVGUUCPFZUUAPFZUUEUUCQUVGUUCUVGYKUVPUUCHFZUVOUVSDUUBVBZ
      SRUVCUWBUVDUUTUUAVFZVCUUCUUAVDSVEUVGUVHUVKDOUVGUVNUVHUVKQZUVTUUBVGZTVHVIU
      VGUVIIJKZYMIUVHJKZGZYNUVHIJKGZVJZUVGUWJUWKUVGYMUWIUVEYKYMVKUVGUVPUWIUVSUU
      BVLZTULVMUVGYKUVHHFZUWHUWLVNUVOUVGUVPUWNUVSUUBVOZTDUVHVPSVRVQVSVTWAWBYJYK
      YPUUNYJYKYPGZGUUMCUUHYJUVAUWPUUMUVBUVEUWPUUMUVFUVEUWPGZUULLDMNZDONZUVKONZ
      WCZIJUWQUULUWRUUBONZUUCWCZONZUXBUUCONZWCZUXAUWQUUJUXBUUKUXCOUWQUUBUUCMNZY
      TUUCMNUUAMNZUXBUUJUWQYTPFZUWBUWAUXGUXHQUVDUXIUVCUWPYTVFZWIZUVCUWBUVDUWPUW
      EVCZUWQUUCUWQYKUVPUWCUVEYKYPUOZUVEUVPUWPUVRUSZUWDSRZYTUUAUUCWJVAUWQUXBLUU
      BONZUUCMNZUXGUWQUVMUVNUXBUXQQZUWQDUXMRZUWQUUBUXNRZLPFZUVMUVNUXRWDLDUUBWEW
      FSUWQUXPUUBUUCMUWQUVNUXPUUBQZUXTUUBWGZTVEWHUWQUXIUWAUWBUXHUUJQUXKUXOUXLYT
      UUCUUAWKVAWLUWQUUAUUAMNZUUCMNZUUAUUCMNUUAMNZUXCUUKUWQUWBUWBUWAUYEUYFQUXLU
      XLUXOUUAUUAUUCWJVAUWQUYEIUUCMNUXCUWQUYDIUUCMUWQUWBUYDIQUXLUUAWSTVEUUCWMWN
      UWQUWBUWAUWBUYFUUKQUXLUXOUXLUUAUUCUUAWKVAWLWOUWQUXBPFUWAUXDUXFQUWQUXBUWQU
      WRHFZUVPUXBHFYKUYGUVEYPLHFZYKUYGWPLDUQZWQWRZUXNUWRUUBVBSRUXOUXBUUCWTSUWQU
      XEUWTUWQUWRPFUVNUVMUVNUXEUWTQUWQUWRUYJRUXTUXSUXTUWRUUBDUUBXIXAXBXCUWQIUWT
      JKZUXAIJKZUWQIUWSUVHONZUWTJUWQUWSHFZIUWSJKZUWNUWIIUYMJKUWQUYGYKUYNUYJUXMU
      WRDVBSZUWPUYOUVEUWPUYGIUWRJKZYKYNUYOUWPUYHYKUYGWPYKYPXDZUYIXEYKYOUYQYNYKU
      YQYOUYHYKUYQYOVNWPLDXFWQXGXHUYRYKYNYOUOUWRDXJXAUHUWQUVPUWNUXNUWOTUWQUVPUW
      IUXNUWMTUWSUVHXJXAUWQUVHUVKUWSOUWQUVNUWFUXTUWGTVHXKUWQUWTHFZUYKUYLVNUWQUY
      NUVKHFZUYSUYPUWQUVPUVPUYTUXNUXNUUBUUBVBSUWSUVKVBSUWTXLTXMVQVSVTWAWBYJYKYQ
      UUSYJYKYQGZGUURCUUHYJUVAVUAUURUVBUVEVUAUURUVFUVEVUAGZUUQDLMNZUVHONZWCZIJV
      UBUUBWCZVUCUUBONZONZUUOVUGONVUEUUQVUBVUFUUOVUGOVUBUXIUWBVUFUUOQUVDUXIUVCV
      UAUXJWIZUVCUWBUVDVUAUWEVCZYTUUAXNSVEVUBVUHUUBVUGONZWCZVUEVUBUVNVUGPFVUHVU
      LQVUBUUBVUBUVDUVCUVPUVCUVDVUAXOUVCUVDVUAXPUVQSZRZVUBVUGVUBVUCHFZUVPVUGHFY
      KVUOUVEYQDXQWRZVUMVUCUUBVBSRUUBVUGXSSVUBVUKVUDVUBVUKVUCUVKONZVUDVUBUVNVUC
      PFUVNVUKVUQQVUNVUBVUCVUPRVUNUUBVUCUUBXRVAVUBUVHUVKVUCOVUBUVNUWFVUNUWGTVHX
      TXBYAVUBVUGUUPUUOOVUBVUGUUCUXPMNZUUCUUBMNZUUPVUBUVMUVNVUGVURQZVUBDUVEYKYQ
      UOZRVUNUVMUYAUVNVUTWDDLUUBWEYBSVUBUXPUUBUUCMVUBUVNUYBVUNUYCTVHVUBUWAUXIUW
      BVUSUUPQVUBUUCVUBYKUVPUWCVVAVUMUWDSRVUIVUJUUCYTUUAYCVAXCVHWLVUBIVUDJKZVUE
      IJKZVUBVUOIVUCJKZUWNUWIVVBVUPVUBVVDYQUVEYKYQVKYKVVDYQVNZUVEYQYKUYHVVEWPDL
      XFYDWRVRVUBUVPUWNVUMUWOTZVUBUVPUWIVUMUWMTVUCUVHXJXAVUBVUDHFZVVBVVCVNVUBVU
      OUWNVVGVUPVVFVUCUVHVBSVUDXLTXMVQVSVTWAWBYEYF $.
      $( [27-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( An algebraic characterization of colinearity.  Note the similarity to
       ~ brbtwn2 .  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    colinearalg $p |-
        (
          ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( ( A Btwn <. B , C >. \/ B Btwn <. C , A >. \/
              C Btwn <. A , B >. ) <->
            A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) $=
      ( cfv wcel wbr cmin co cmul cc0 cle wral wceq wa wb cc syl2anc vp cee w3a
      cop cbtwn w3o cv cfz brbtwn2 3comr colinearalglem3 anbi2d colinearalglem2
      c1 bitrd 3coml 3orbi123d wi fveecn subid opreq2d adantl subcl mul01 eqtrd
      syl syl2an anandirs 0re leidi syl6eqbr ralrimiva 3adant1 opreq12d ralbidv
      fveq1 breq1d syl5ibcom 3mix1 syl6 a1dd wne wrex wn simp3 simp1 necon3abid
      eqeefv df-ne rexbii rexnal bitr2i syl6bb ralcom weq fveq2 opreq1d eqeq12d
      rcla4v ad2antrl cr fveere 3ad2antl1 3ad2antl2 3ad2antl3 3jca anim1i caddc
      anasss cdiv adantlr 3anim123i adantr ad2antlr simplrr eqcom simp12 simp11
      recn simp22 simp21 simp23 simpr3 simpr1 subeq0 necon3bid biimp3ar syl3anc
      divcl mulcl syl112anc syl5bb resubcl opreq1 ralimi ralbi opreq2 wo df-3or
      andir simp13 bicomd div23 eqeq2d divmul mulcom eqeq1d 3simpb simpl2 recnd
      subadd2 3bitr2d ralbidva simpl1 simpl3 biimpar colinearalglem4 syl5ibrcom
      3ad2ant1 redivcl sylbird syldan syl5bi rexlimdv sylbid pm2.61dne pm4.71rd
      syld exp32 orbi1i anbi1i bitri 3bitr4i syl6rbb ) AFUBGZHZBUVOHZCUVOHZUCZA
      BCUDUEIZBCAUDUEIZCABUDUEIZUFDUGZBGZUWCAGZJKZUWCCGZUWEJKZLKZMNIZDUNFUHKZOZ
      UWFEUGZCGZUWMAGZJKZLKZUWMBGZUWOJKZUWHLKZPZEUWKODUWKOZQZUWGUWDJKZUWEUWDJKZ
      LKZMNIZDUWKOZUXBQZUWEUWGJKZUWDUWGJKZLKZMNIZDUWKOZUXBQZUFZUXBUVSUVTUXCUWAU
      XIUWBUXOABCDEFUIUVSUWAUXHUXDUWOUWRJKLKUWNUWRJKUXELKPEUWKODUWKOZQZUXIUVQUV
      RUVPUWAUXRRBCADEFUIUJUVSUXQUXBUXHUVQUVRUVPUXQUXBRBCADEFUKUJULUOUVRUVPUVQU
      WBUXORUVRUVPUVQUCZUWBUXNUXJUWRUWNJKLKUWOUWNJKUXKLKPEUWKODUWKOZQUXOCABDEFU
      IUXSUXTUXBUXNCABDEFUMULUOUPUQUVSUXBUWLUXHUXNUFZUXBQZUXPUVSUXBUYAUVSUXBUYA
      URZCAUVSCAPZUYAUXBUVSUYDUWLUYAUVSUXKUWGUWGJKZLKZMNIZDUWKOZUYDUWLUVQUVRUYH
      UVPUVQUVRQZUYGDUWKUYIUWCUWKHZQUYFMMNUVQUVRUYJUYFMPZUVQUYJQUWDSHZUWGSHZUYK
      UVRUYJQBUWCFUSZCUWCFUSZUYLUYMQZUYFUXKMLKZMUYMUYFUYQPUYLUYMUYEMUXKLUWGUTVA
      VBUYPUXKSHUYQMPUWDUWGVCUXKVDVFVEVGVHMVIVJVKVLVMUYDUYGUWJDUWKUYDUYFUWIMNUY
      DUXKUWFUYEUWHLUYDUWGUWEUWDJUWCCAVPZVAUYDUWGUWEUWGJUYRVAVNVQVOVRUWLUXHUXNV
      SVTWAUVSCAWBZUAUGZCGZUYTAGZWBZUAUWKWCZUYCUVSUYSVUAVUBPZUAUWKOZWDZVUDUVSVU
      FCAUVSUVRUVPUYDVUFRUVPUVQUVRWEUVPUVQUVRWFCAUAFWHTWGVUDVUEWDZUAUWKWCVUGVUC
      VUHUAUWKVUAVUBWIWJVUEUAUWKWKWLWMUVSVUCUYCUAUWKUVSUYTUWKHZVUCUYCUXBUXADUWK
      OZEUWKOZUVSVUIVUCQZQZUYAUXADEUWKUWKWNVUMVUKUWFVUAVUBJKZLKZUYTBGZVUBJKZUWH
      LKZPZDUWKOZUYAVUIVUKVUTURUVSVUCVUJVUTEUYTUWKEUAWOZUXAVUSDUWKVVAUWQVUOUWTV
      URVVAUWPVUNUWFLVVAUWNVUAUWOVUBJUWMUYTCWPUWMUYTAWPZVNVAVVAUWSVUQUWHLVVAUWR
      VUPUWOVUBJUWMUYTBWPVVBVNWQWRVOWSWTUVSVULVUBXAHZVUPXAHZVUAXAHZUCZVUCQZVUTU
      YAURUVSVUIVUCVVGUVSVUIQZVVFVUCVVHVVCVVDVVEUVPUVQVUIVVCUVRAUYTFXBXCUVQUVPV
      UIVVDUVRBUYTFXBXDUVRUVPVUIVVEUVQCUYTFXBXEXFXGXIUVSVVGQZVUTUWDVUQVUNXJKZUW
      HLKZUWEXHKZPZDUWKOZUYAVVIVVMVUSDUWKVVIUYJQUWESHZUYLUYMUCZVUBSHZVUPSHZVUAS
      HZUCZVUCVVMVUSRUVSUYJVVPVVGUVSUYJQVVOUYLUYMUVPUVQUYJVVOUVRAUWCFUSXCUVQUVP
      UYJUYLUVRUYNXDUVRUVPUYJUYMUVQUYOXEXFXKVVGVVTUVSUYJVVFVVTVUCVVCVVQVVDVVRVV
      EVVSVUBXSZVUPXSVUAXSXLXMXNUVSVVFVUCUYJXOVVMVVLUWDPZVVPVVTVUCUCZVUSUWDVVLX
      PVWCVWBUWFVVKPZUWFVURVUNXJKZPZVUSVWCUYLVVOVVKSHZVWBVWDRVVOUYLUYMVVTVUCXQZ
      VVOUYLUYMVVTVUCXRZVWCVVJSHZUWHSHZVWGVWCVUQSHZVUNSHZVUNMWBZVWJVWCVVRVVQVWL
      VVPVVQVVRVVSVUCXTVVPVVQVVRVVSVUCYAZVUPVUBVCTZVWCVVSVVQVWMVVPVVQVVRVVSVUCY
      BVWOVUAVUBVCTZVVPVVTVWNVUCVVPVVTQZVUNMVUAVUBVWRVVSVVQVUNMPVUERZVVPVVQVVRV
      VSYCVVPVVQVVRVVSYDVUAVUBYEZTYFYGZVUQVUNYIYHVWCUYMVVOVWKVVOUYLUYMVVTVUCUUA
      VWIUWGUWEVCTZVVJUWHYJTUYLVVOVWGUCVWDVWBUWDUWEVVKUUKUUBYHVWCVWEVVKUWFVWCVW
      LVWKVWMVWNVWEVVKPVWPVXBVWQVXAVUQUWHVUNUUCYKUUDVWFVWEUWFPZVWCVUSUWFVWEXPVW
      CVXCVUNUWFLKZVURPZVUSVWCVURSHZUWFSHZVWMVWNVXCVXERVWCVWLVWKVXFVWPVXBVUQUWH
      YJTVWCUYLVVOVXGVWHVWIUWDUWEVCTZVWQVXAVURUWFVUNUUEYKVWCVXDVUOVURVWCVWMVXGV
      XDVUOPVWQVXHVUNUWFUUFTUUGUOYLUULYLYHUUMUVSUVPUVRQZVVJXAHZVVNUYAURVVGUVPUV
      QUVRUUHVVGVUQXAHZVUNXAHZVWNVXJVVGVVDVVCVXKVVCVVDVVEVUCUUIVVCVVDVVEVUCUUNZ
      VUPVUBYMTVVGVVEVVCVXLVVCVVDVVEVUCUUOVXMVUAVUBYMTVVFVWNVUCVVFVUNMVUAVUBVVF
      VVSVVQVWSVVFVUAVVCVVDVVEWEUUJVVCVVDVVQVVEVWAUUSVWTTYFUUPVUQVUNUUTYHVXIVXJ
      QUYAVVNVVLUWEJKZUWHLKZMNIZDUWKOZUWGVVLJKZUWEVVLJKZLKZMNIZDUWKOZUXJVVLUWGJ
      KZLKZMNIZDUWKOZUFACDVVJFUUQVVNUWLVXQUXHVYBUXNVYFVVNUWJVXPRZDUWKOUWLVXQRVV
      MVYGDUWKVVMUWIVXOMNVVMUWFVXNUWHLUWDVVLUWEJYNWQVQYOUWJVXPDUWKYPVFVVNUXGVYA
      RZDUWKOUXHVYBRVVMVYHDUWKVVMUXFVXTMNVVMUXDVXRUXEVXSLUWDVVLUWGJYQUWDVVLUWEJ
      YQVNVQYOUXGVYADUWKYPVFVVNUXMVYERZDUWKOUXNVYFRVVMVYIDUWKVVMUXLVYDMNVVMUXKV
      YCUXJLUWDVVLUWGJYNVAVQYOUXMVYEDUWKYPVFUQUURVGUVAUVBUVHUVCUVIUVDUVEUVFUVGU
      WLUXHYRZUXBQZUXOYRZUXCUXIYRZUXOYRUYBUXPVYKVYMUXOUWLUXHUXBYTUVJUYBVYJUXNYR
      ZUXBQVYLUYAVYNUXBUWLUXHUXNYSUVKVYJUXNUXBYTUVLUXCUXIUXOYSUVMUVNUO $.
      $( [24-Jun-2013] $)
  $}


  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesub.1 $e |- C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  (Contributed
       by Scott Fenton, 17-Jul-2013.) $)
    eleesub $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel wa c1 cfz co cv cmin cmpt cr wral fveere resubcl ralrimiva
      syl2an anandirs wb cn eleenn mptelee syl adantr mpbird syl5eqel ) AEGHZIZ
      BULIZJZCDKELMZDNZAHZUQBHZOMZPZULFUOVAULIZUTQIZDUPRZUOVCDUPUMUNUQUPIZVCUMV
      EJURQIUSQIVCUNVEJAUQESBUQESURUSTUBUCUAUMVBVDUDZUNUMEUEIVFAEUFURUSDOEUGUHU
      IUJUK $.
      $( [17-Jul-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesubd.1 $e |- ( ph ->
       C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  Deduction
       form of ~ eleesub .  (Contributed by Scott Fenton, 17-Jul-2013.) $)
    eleesubd $p |- ( ( ph /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel w3a c1 cfz co cv cmin cmpt wa cr fveere wceq 3ad2ant1 wral
      resubcl syl2an anandirs ralrimiva wb cn eleenn mptelee syl adantr 3adant1
      mpbird eqeltrd ) ABFHIZJZCUQJZKDELFMNZEOZBIZVACIZPNZQZUQAURDVEUAUSGUBURUS
      VEUQJZAURUSRZVFVDSJZEUTUCZVGVHEUTURUSVAUTJZVHURVJRVBSJVCSJVHUSVJRBVAFTCVA
      FTVBVCUDUEUFUGURVFVIUHZUSURFUIJVKBFUJVBVCEPFUKULUMUOUNUP $.
      $( [17-Jul-2013] $)
  $}

