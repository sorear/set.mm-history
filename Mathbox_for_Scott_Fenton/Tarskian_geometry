$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Tarskian geometry
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c EE Btwn Cgr $.

  $( Declare the syntax for the Euclidean space generator. $)
  cee $a class EE $.

  $( Declare the syntax for the Euclidean betweenness predicate. $)
  cbtwn $a class Btwn $.

  $( Declare the syntax for the Euclidean congruence predicate. $)
  ccgr $a class Cgr $.

  $( Define the Euclidean space generator.  For details, see ~ elee . $)
  df-ee $a |- EE = ( n e. NN |-> ( RR ^m ( 1 ... n ) ) ) $.

  ${
    $d n x y z t i $.
    $( Define the Euclidean betweenness predicate.  For details, see
       ~ brbtwn . $)
    df-btwn $a |- Btwn =
      `' { <. <. x , z >. , y >. |
         E. n e. NN (
           ( x e. ( EE ` n ) /\ z e. ( EE ` n ) /\ y e. ( EE ` n ) ) /\
           E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... n )
             ( y ` i ) = ( ( ( 1 - t ) x. ( x ` i ) ) + ( t x. ( z ` i ) ) ) )
         } $.
  $}

  ${
    $d n x y i $.
    $( Define the Euclidean congruence predicate.  For details, see
       ~ brcgr . $)
    df-cgr $a |- Cgr =
       { <. x , y >. |
         E. n e. NN
           ( ( x e. ( ( EE ` n ) X. ( EE ` n ) ) /\
             y e. ( ( EE ` n ) X. ( EE ` n ) ) ) /\
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` x ) ` i ) - ( ( 2nd ` x ) ` i ) ) ^ 2 ) =
           sum_ i e. ( 1 ... n )
             ( ( ( ( 1st ` y ) ` i ) - ( ( 2nd ` y ) ` i ) ) ^ 2 ) ) } $.
  $}

  ${
    $d N n $.
    $( Membership in a Euclidean space.  We define Euclidean space here using
       Cartesian coordinates over ` N ` space.  We later abstract away from
       this using Tarski's geometry axioms, so this exact definition is
       unimportant.  (Contributed by Scott Fenton, 3-Jun-2013.) $)
    elee $p |- ( N e. NN -> ( A e. ( EE ` N ) <-> A : ( 1 ... N ) --> RR ) ) $=
      ( vn cn wcel cee cfv cr c1 cfz co cmap wf cv wceq opreq2 opreq2d df-ee
      oprex fvmpt eleq2d reex elmap syl6bb ) BDEZABFGZEAHIBJKZLKZEUGHAMUEUFUHAC
      BHICNZJKZLKUHDFUIBOUJUGHLUIBIJPQCRHUGLSTUAHUGAUBIBJSUCUD $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N a k $.  $d A a $.  $d B a $.  $d F a $.
    $( A condition for a mapping to be an element of a Euclidean space.
       (Contributed by Scott Fenton, 7-Jun-2013.) $)
    mptelee $p |- ( N e. NN ->
      ( ( k e. ( 1 ... N ) |-> ( A F B ) ) e. ( EE ` N ) <->
        A. k e. ( 1 ... N ) ( A F B ) e. RR ) ) $=
      ( va cn wcel c1 cfz co cmpt cee cfv cr wf wral elee crn wss wfn df-f cvv
      cv oprex a1i rgen eqid mptfng mpbi mpbiran wceq wrex cab rnmpt sseq1i wi
      wal abss hbre1 ax-17 hbim hbal r19.23v albii bicomi ralcom4 ra4 clel2
      biimpri syld sylbi r19.21ai hbra1 eleq1a syl6 r19.23ad 19.21aiv impbii
      bitri syl6bb ) EGHCIEJKZABDKZLZEMNHWBOWDPZWCOHZCWBQZWDERWEWDSZOTZWGWEWDWB
      UAZWIWBOWDUBWCUCHZCWBQWJWKCWBWKCUDWBHZABDUEZUFUGCWBWCWDWDUHZUIUJUKWIFUDZW
      CULZCWBUMZFUNZOTZWGWHWROCFWBWCWDWNUOUPWSWQWOOHZUQZFURZWGWQFOUSXBWGXBWFCWB
      XACFWQWTCWPCWBUTWTCVAZVBVCXBWPWTUQZCWBQZFURZWLWFUQZXFXBXEXAFWPWTCWBVDVEVF
      XFXDFURZCWBQZXGXIXFXDCFWBVGVFXIWLXHWFXHCWBVHXHWFUQXIWFXHFWCOWMVIVJUFVKVLV
      LVMWGXAFWGWPWTCWBWFCWBVNXCWGWLWFXDWFCWBVHWCOWOVOVPVQVRVSVTVTVTWA $.
      $( [7-Jun-2013] $)
  $}

  ${
    $( If ` A ` is in ` ( EE `` N ) ` , then ` N ` is a natural.  (Contributed
       by Scott Fenton, 1-Jul-2013.) $)
    eleenn $p |- ( A e. ( EE ` N ) -> N e. NN ) $=
      ( vn cee cfv wcel c0 wceq wn cn n0i cdm cr c1 cv cfz co cmap cmpt df-ee
      dmeqi cvv dmmpt oprex a1i mprg eqtri eleq2i notbii biimpri ndmfv syl
      con1i ) ABDEZFUNGHZIBJFZUNAKUPUOUPIZBDLZFZIZUOUTUQUSUPURJBURCJMNCOZPQZRQZ
      SZLZJDVDCTUAVCUBFZVEJHCJCJVCUBUCVFVAJFMVBRUDUEUFUGUHUIUJBDUKULUMUL $.
      $( [1-Jul-2013] $)
  $}

  $( The forward direction of ~ elee .  (Contributed by Scott Fenton,
     1-Jul-2013.) $)
  eleei $p |- ( A e. ( EE ` N ) -> A : ( 1 ... N ) --> RR ) $=
    ( cee cfv wcel c1 cfz co cr wf cn wb eleenn elee syl ibi ) ABCDEZFBGHIAJZQB
    KEQRLABMABNOP $.
    $( [1-Jul-2013] $)

  $( A point belongs to at most one Euclidean space.  (Contributed by Scott
     Fenton, 1-Jul-2013.) $)
  eedimeq $p |- ( ( A e. ( EE ` N ) /\ A e. ( EE ` M ) ) -> N = M ) $=
    ( cee cfv wcel wa c1 wceq cfz co cr wf eleei cdm fdm sylan9req syl2an cuz
    cn wb eleenn nnuz syl6eleq fzopth mpbid simprd ) ACDEFZABDEFZGZHHIZCBIZUJHC
    JKZHBJKZIZUKULGZUHUMLAMZUNLAMZUOUIACNABNUQURUMAOUNUMLAPUNLAPQRUHCHSEZFBTFUO
    UPUAUIUHCTUSACUBUCUDABUBTHBHCUERUFUG $.
    $( [1-Jul-2013] $)

  ${
    $d N a n x y z i t $.  $d A x y z i t n $.  $d B x y z i t n $.
    $d C x y z i t n $.
    $( The binary relationship form of the betweenness predicate.  The
       statement ` A Btwn <. B , C >. ` should be informally read as " ` A `
       lies on a line segment between ` B ` and ` C ` .  This exact definition
       is abstracted away by Tarski's geometry axioms later on.  (Contributed
       by Scott Fenton, 3-Jun-2013.) $)
    brbtwn $p |- (
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
      ( A Btwn <. B , C >. <->
        E. t e. ( 0 [,] 1 ) A. i e. ( 1 ... N )
           ( A ` i ) =
           ( ( ( 1 - t ) x. ( B ` i ) ) + ( t x. ( C ` i ) ) ) ) ) $=
      ( vy vn vz vx cop cbtwn wbr cv cee cfv wcel w3a c1 cmin co cmul caddc
      wceq cfz wral cc0 cicc wrex wa cn copab2 ccnv df-btwn breqi wb cvv opex
      brcnvg mpan2 3ad2ant1 df-br eleq1 3anbi1d fveq1 opreq2d opreq1d eqeq2d
      rexralbidv anbi12d rexbidv 3anbi2d 3anbi3d eqeq1d eloprabg wi simp1
      eedimeq ancoms syl2an opreq2 raleqdv syl biimpd expimpd a1d r19.23adv
      eleenn fveq2 eleq2d 3anbi123d rcla4ev exp32 mpcom impbid bitrd 3comr
      syl5bb ) BCDKZLMBWSGNZHNZOPZQZINZXBQZJNZXBQZRZENZXFPZSANZTUAZXIWTPZUBUAZX
      KXIXDPZUBUAZUCUAZUDZESXAUEUAZUFAUGSUHUAZUIZUJZHUKUIZGIJULZUMZMZBFOPZQZCYG
      QZDYGQZRZXIBPZXLXICPZUBUAZXKXIDPZUBUAZUCUAZUDZESFUEUAZUFZAXTUIZBWSLYEGJIA
      EHUNUOYKYFWSBYDMZUUAYHYIYFUUBUPZYJYHWSUQQUUCCDURBWSYGUQYDUSUTVAUUBWSBKYDQ
      ZYKUUAWSBYDVBYIYJYHUUDUUAUPYIYJYHRZUUDCXBQZDXBQZBXBQZRZYREXSUFZAXTUIZUJZH
      UKUIZUUAYCUUFXEXGRZXJYNXPUCUAZUDZEXSUFAXTUIZUJZHUKUIUUFUUGXGRZXJYQUDZEXSU
      FAXTUIZUJZHUKUIUUMGIJCDBYGYGYGWTCUDZYBUURHUKUVCXHUUNYAUUQUVCXCUUFXEXGWTCX
      BVCVDUVCXRUUPAEXTXSUVCXQUUOXJUVCXNYNXPUCUVCXMYMXLUBXIWTCVEVFVGVHVIVJVKXDD
      UDZUURUVBHUKUVDUUNUUSUUQUVAUVDXEUUGUUFXGXDDXBVCVLUVDUUPUUTAEXTXSUVDUUOYQX
      JUVDXPYPYNUCUVDXOYOXKUBXIXDDVEVFVFVHVIVJVKXFBUDZUVBUULHUKUVEUUSUUIUVAUUKU
      VEXGUUHUUFUUGXFBXBVCVMUVEUUTYRAEXTXSUVEXJYLYQXIXFBVEVNVIVJVKVOUUEUUMUUAUU
      EUULUUAHUKUUEUULUUAVPXAUKQUUEUUIUUKUUAUUEUUIUJZUUKUUAUVFXAFUDZUUKUUAUPUUE
      YIUUFUVGUUIYIYJYHVQUUFUUGUUHVQUUFYIUVGCFXAVRVSVTUVGUUJYTAXTUVGYREXSYSXAFS
      UEWAWBVKZWCWDWEWFWGFUKQZUUEUUAUUMVPYIYJUVIYHCFWHVAUVIUUEUUAUUMUULUUEUUAUJ
      HFUKUVGUUIUUEUUKUUAUVGUUFYIUUGYJUUHYHUVGXBYGCXAFOWIZWJUVGXBYGDUVJWJUVGXBY
      GBUVJWJWKUVHVJWLWMWNWOWPWQWRWPWR $.
      $( [3-Jun-2013] $)
  $}

  ${
    $d N n i x y $.  $d A n i x y $.  $d B n i x y $.  $d C n i x y $.
    $d D n i x y $.
    $( The binary relationship form of the congruence predicate.  The statement
       ` <. A , B >. Cgr <. C , D >. ` should be read informally as "the ` N `
       dimensional point ` A ` is as far from ` B ` as ` C ` is from ` D ` , or
       "the line segment ` A B ` is congruent to the line segment ` C D ` .
       This particular definition is encapsulated by Tarski's axioms later on.
       (Contributed by Scott Fenton, 3-Jun-2013.) $)
    brcgr $p |- ( ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
                    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. Cgr <. C , D >. <->
        sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) =
        sum_ i e. ( 1 ... N ) ( ( ( C ` i ) - ( D ` i ) ) ^ 2 ) ) ) $=
      ( vn vx vy cop ccgr wbr cv cee cfv cxp wcel wa c1 cfz co c1st c2nd cmin
      c2 cexp csu wceq cn wrex opex eleq1 anbi1d fveq2 fveq1d opreq12d opreq1d
      sumeq2sdv eqeq1d anbi12d rexbidv anbi2d eqeq2d df-cgr brab wb opelxpg
      simplbda adantll ad2ant2l simplrr eedimeq syl11anc adantlr opreq2
      sumeq1d eqeq12d syl op1stg adantr op2ndg eqeqan12d ad2antrr bitrd biimpd
      expimpd r19.23adva wi eleenn ad2antll opelxpi anim12i biimpar jca xpeq12
      eleq2d rcla4ev sylan2 exp32 mpcom impbid syl5bb ) ABJZCDJZKLXCGMZNOZXFPZQ
      ZXDXGQZRZSXETUAZEMZXCUBOZOZXLXCUCOZOZUDUAZUEUFUAZEUGZXKXLXDUBOZOZXLXDUCOZ
      OZUDUAZUEUFUAZEUGZUHZRZGUIUJZAFNOZQZBYJQZRZCYJQZDYJQZRZRZSFTUAZXLAOZXLBOZ
      UDUAZUEUFUAZEUGZYRXLCOZXLDOZUDUAZUEUFUAZEUGZUHZHMZXGQZIMZXGQZRZXKXLUUJUBO
      ZOZXLUUJUCOZOZUDUAZUEUFUAZEUGZXKXLUULUBOZOZXLUULUCOZOZUDUAZUEUFUAZEUGZUHZ
      RZGUIUJXHUUMRZXSUVHUHZRZGUIUJYIHIXCXDKABUKCDUKUUJXCUHZUVJUVMGUIUVNUUNUVKU
      VIUVLUVNUUKXHUUMUUJXCXGULUMUVNUVAXSUVHUVNXKUUTXREUVNUUSXQUEUFUVNUUPXNUURX
      PUDUVNXLUUOXMUUJXCUBUNUOUVNXLUUQXOUUJXCUCUNUOUPUQURUSUTVAUULXDUHZUVMYHGUI
      UVOUVKXJUVLYGUVOUUMXIXHUULXDXGULVBUVOUVHYFXSUVOXKUVGYEEUVOUVFYDUEUFUVOUVC
      YAUVEYCUDUVOXLUVBXTUULXDUBUNUOUVOXLUVDYBUULXDUCUNUOUPUQURVCUTVAHIEGVDVEYQ
      YIUUIYQYHUUIGUIYQXEUIQZRZXJYGUUIUVQXJRZYGUUIUVRYGYRXREUGZYRYEEUGZUHZUUIUV
      RXEFUHZYGUWAVFYQXJUWBUVPYQXJRDXFQZYOUWBYPXIUWCYMXHYOXIUWCYNYOXICXFQUWCCDX
      FXFYJVGVHVIVJYMYNYOXJVKDFXEVLVMVNUWBXSUVSYFUVTUWBXKYRXREXEFSTVOZVPUWBXKYR
      YEEUWDVPVQZVRYQUWAUUIVFUVPXJYMYPUVSUUCUVTUUHYMYRXRUUBEYMXQUUAUEUFYMXNYSXP
      YTUDYKXNYSUHYLYKXLXMAABYJVSUOVTYMXLXOBABYJYJWAUOUPUQURYPYRYEUUGEYPYDUUFUE
      UFYPYAUUDYCUUEUDYPXLXTCYNXTCUHYOCDYJVSVTUOYPXLYBDCDYJYJWAUOUPUQURWBZWCWDW
      EWFWGFUIQZYQUUIYIWHYOUWGYMYNDFWIWJUWGYQUUIYIYQUUIRZUWGXCYJYJPZQZXDUWIQZRZ
      UWARZYIUWHUWLUWAYQUWLUUIYMUWJYPUWKABYJYJWKCDYJYJWKWLVTYQUWAUUIUWFWMWNYHUW
      MGFUIUWBXJUWLYGUWAUWBXHUWJXIUWKUWBXGUWIXCUWBXFYJUHZUWNXGUWIUHXEFNUNZUWOXF
      YJXFYJWOVMZWPUWBXGUWIXDUWPWPUTUWEUTWQWRWSWTXAXB $.
      $( [3-Jun-2013] $)
  $}

  $( The function value of a point is a real.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveere $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. RR ) $=
    ( cee cfv wcel c1 cfz co cr wf eleei ffvelrn sylan ) ACDEFGCHIZJAKBOFBAEJFA
    CLOJBAMN $.
    $( [10-Jun-2013] $)

  $( The function value of a point is a complex.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  fveecn $p |- ( ( A e. ( EE ` N ) /\ I e. ( 1 ... N ) ) ->
         ( A ` I ) e. CC ) $=
    ( cee cfv wcel c1 cfz co wa fveere recnd ) ACDEFBGCHIFJBAEABCKL $.
    $( [10-Jun-2013] $)

  ${
    $d A i $.  $d B i $.  $d N i $.
    $( Two points are equal iff they agree in all dimensions.  (Contributed by
       Scott Fenton, 10-Jun-2013.) $)
    eqeefv $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
       ( A = B <-> A. i e. ( 1 ... N ) ( A ` i ) = ( B ` i ) ) ) $=
      ( cee cfv wcel c1 cfz co wfn wceq cv wral wb cr wf eleei ffn syl eqfnfv2
      syl2an ) ADEFZGZAHDIJZKZBUEKZABLCMZAFUHBFLCUENOBUCGZUDUEPAQUFADRUEPASTUIU
      EPBQUGBDRUEPBSTCUEABUAUB $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    $( Two points are equal iff the square of the distance between them is
       zero.  (Contributed by Scott Fenton, 10-Jun-2013.) $)
    eqeelen $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        ( A = B <->
          sum_ i e. ( 1 ... N ) ( ( ( A ` i ) - ( B ` i ) ) ^ 2 ) = 0 ) ) $=
      ( cee cfv wcel wa cv wceq c1 cfz co wral cmin c2 cexp cc0 csu wb cc
      fveecn subcl sqeq0 syl subeq0 bitrd syl2an anandirs ralbidva bicomd
      eqeefv cuz cr cle wbr cn eleenn nnuz syl6eleq adantr fveere resubcl
      resqcl sqge0 jca r19.21aiva fsum00 syl11anc 3bitr4d ) ADEFZGZBVKGZHZCIZAF
      ZVOBFZJZCKDLMZNZVPVQOMZPQMZRJZCVSNZABJVSWBCSRJZVNWDVTVNWCVRCVSVLVMVOVSGZW
      CVRTZVLWFHZVPUAGZVQUAGZWGVMWFHZAVODUBBVODUBWIWJHZWCWARJZVRWLWAUAGWCWMTVPV
      QUCWAUDUEVPVQUFUGUHUIUJUKABCDULVNDKUMFZGZWBUNGZRWBUOUPZHZCVSNWEWDTVLWOVMV
      LDUQWNADURUSUTVAVNWRCVSVLVMWFWRWHVPUNGZVQUNGZWRWKAVODVBBVODVBWSWTHWAUNGZW
      RVPVQVCXAWPWQWAVDWAVEVFUEUHUIVGWBCKDVHVIVJ $.
      $( [10-Jun-2013] $)
  $}

  ${
    $d N i $.  $d M i $.
    $( Useful lemma going forward.  The finite sum of a set of squares of reals
       is non-negative.  (Contributed by Scott Fenton, 11-Jun-2013.) $)
    fsumsq $p |- ( ( N e. ( ZZ>= ` M ) /\ A. i e. ( M ... N ) A e. RR ) ->
       0 <_ sum_ i e. ( M ... N ) ( A ^ 2 ) ) $=
      ( cr wcel cfz co wral cuz cfv c2 cexp cc0 cle wbr wa csu resqcl sqge0
      jca ralimi fsumcmp0 sylan2 ) AEFZBCDGHZIDCJKFALMHZEFZNUGOPZQZBUFINUFUGBRO
      PUEUJBUFUEUHUIASATUAUBUGBCDUCUD $.
      $( [11-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( Alternate characterization of betweenness, with no existential
       quantifiers.  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    brbtwn2 $p |-
        ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( A Btwn <. B , C >. <->
            ( A. i e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 /\
              A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) ) $=
      ( vk vt vp cee cfv wcel w3a cop cbtwn wbr cv c1 cmin co cmul caddc wceq
      cfz wral cc0 cicc wrex cle wa brbtwn wi cr fveere 3ad2antl2 3ad2antl3
      jca cneg c2 cexp cc resubcl 3adant3 recnd sqval syl opreq2d 0re 1re
      elicc2i simp1bi 3ad2ant3 sylancr negcl mul4 syl22anc recn 3ad2ant1
      3ad2ant2 subdi syl111anc ax-1cn subdir mp3an1 syl11anc nncan opreq1d
      mulid2 3eqtr3d simp1 remulcl simp2 subsub4 3eqtrd mulneg1 eqtrd subsub3
      negeqd readdcl negsubdi2 opreq12d 3eqtr2rd mulneg2 resqcl simp2bi
      simp3bi wb subge0 mpbird mulge0 sqge0 le0neg2 mpbid eqbrtrd 3expa sylan
      an32s r19.21aiva fveecn anim12i anandirs anandis 3adantl1 subcl ancoms
      mulcom simp2r simp2l simp1l simp1r mulsub2 simp3 mpan mulcl sub23
      3eqtr3rd 3eqtr2d syl2an r19.21aivva weq fveq2 eqeq12d rcla4cva opreq2
      breq1d ralbidva opreqan12d opreqan12rd 2ralbidva anbi12d biimprcd
      r19.23adva wo 3ad2antl1 mulsuble0b anbi1d simpl2 simpl1 eqeefv adantlr
      letri3 pm4.25 fveq1 breq2d anbi2d orbi12d ad2antlr syl5bb bitrd biimprd
      adantrd ex 0elunit 3jca mul02 addid1 adantr 3adant1 ad2antll eqtr2d
      subid1i syl6eq opreq1 eqeq2d ralbidv rcla4ev exp32 syldd wne wn
      necon3abid df-ne rexbii rexnal bitri syl6bbr cdiv breq12d rcla4v
      ad2antrl simprr simpl simprl lesub1 clt ltlen letr imp simplrr necomd
      mpbir2and posdif divelunit div2sub syl23anc lesub2 eqeltrd jaodan syld
      rcla42v simp11 sylancOLD simp12 simp13 simpl23 simpl21 simpl12 simpl22
      simpl13 simpl3 subeq0 necon3bid divdir syl112anc npncan2 adddir simpl11
      add32 addid2 addsub nnncan2 eqtr3d addsubass eqtr4d 3eqtr4d simpr adddi
      npcan addcl divmul div23 divsubdir divid syl331anc 3expia com23
      r19.21adv anim12d syl6 r19.23adv sylbid pm2.61dne impbid ) AFJKZLZBVVDLZC
      VVDLZMZABCNOPGQZAKZRHQZSTZVVIBKZUATZVVKVVICKZUATZUBTZUCZGRFUDTZUEZHUFRUGT
      ZUHZDQZBKZVWCAKZSTZVWCCKZVWESTZUATZUFUIPZDVVSUEZVWFEQZCKZVWLAKZSTZUATZVWL
      BKZVWNSTZVWHUATZUCZEVVSUEDVVSUEZUJZHABCGFUKVVHVWBVXBVVHVVTVXBHVWAVVHVVKVW
      ALZUJZVWDVVLVWDUATZVVKVWGUATZUBTZSTZVWGVXGSTZUATZUFUIPZDVVSUEZVXHVWMVVLVW
      QUATZVVKVWMUATZUBTZSTZUATZVWQVXOSTZVXIUATZUCZEVVSUEDVVSUEZVVTVXBULVXDVXKD
      VVSVVHVWCVVSLZVXCVXKVVHVYBUJZVWDUMLZVWGUMLZUJVXCVXKVYCVYDVYEVVFVVEVYBVYDV
      VGBVWCFUNUOZVVGVVEVYBVYEVVFCVWCFUNUPZUQVYDVYEVXCVXKVYDVYEVXCMZVXJVVKVVLUR
      ZUATZVWDVWGSTZUSUTTZUATZUFUIVYHVYMVYJVYKVYKUATZUATZVVKVYKUATZVYIVYKUATZUA
      TZVXJVYHVYLVYNVYJUAVYHVYKVALZVYLVYNUCVYHVYKVYDVYEVYKUMLZVXCVWDVWGVBVCZVDZ
      VYKVEVFVGVYHVVKVALZVYSVYIVALZVYSVYRVYOUCVXCVYDWUCVYEVXCVVKVXCVVKUMLZUFVVK
      UIPZVVKRUIPZUFRVVKVHVIVJZVKZVDZVLZWUBVYHVVLVALZWUDVYHVVLVXCVYDVVLUMLZVYEV
      XCRUMLZWUEWUMVIWUIRVVKVBVMZVLZVDZVVLVNVFWUBVVKVYKVYIVYKVOVPVYHVYPVXHVYQVX
      IUAVYHVYPVVKVWDUATZVXFSTZVWDVXESTZVXFSTZVXHVYHWUCVWDVALZVWGVALZVYPWUSUCZW
      UKVYDVYEWVBVXCVWDVQVRZVYEVYDWVCVXCVWGVQVSZVVKVWDVWGVTZWAVYHWURWUTVXFSVYHR
      VVLSTZVWDUATZRVWDUATZVXESTZWURWUTVYHWULWVBWVIWVKUCZWUQWVERVALZWULWVBWVLWB
      RVVLVWDWCWDZWEVYHWVHVVKVWDUAVYHWVMWUCWVHVVKUCZWBWUKRVVKWFZVMWGVYHWVJVWDVX
      ESVYHWVBWVJVWDUCZWVEVWDWHZVFWGWIWGVYHWVBVXEVALZVXFVALZWVAVXHUCZWVEVYHVXEV
      YHWUMVYDVXEUMLZWUPVYDVYEVXCWJVVLVWDWKWEZVDZVYHVXFVYHWUEVYEVXFUMLZVXCVYDWU
      EVYEWUIVLZVYDVYEVXCWLVVKVWGWKWEZVDZVWDVXEVXFWMZWAWNVYHVYQVVLVYKUATZURZVXG
      VWGSTZURZVXIVYHWULVYSVYQWWKUCWUQWUBVVLVYKWOWEVYHWWJWWLVYHWWJVXEVVLVWGUATZ
      STZVXEVWGVXFSTZSTZWWLVYHWULWVBWVCWWJWWOUCWUQWVEWVFVVLVWDVWGVTWAVYHWWNWWPV
      XESVYHWWNRVWGUATZVXFSTZWWPVYHWUCWVCWWNWWSUCZWUKWVFWVMWUCWVCWWTWBRVVKVWGWC
      WDZWEVYHWWRVWGVXFSVYHWVCWWRVWGUCZWVFVWGWHZVFWGWPVGVYHWVSWVCWVTWWQWWLUCWWD
      WVFWWHVXEVWGVXFWQWAWNWRVYHVXGVALWVCWWMVXIUCVYHVXGVYHWWBWWEVXGUMLWWCWWGVXE
      VXFWSWEVDWVFVXGVWGWTWEWNXAXBVYHVYMVVKVVLUATZVYLUATZURZUFUIVYHVYMWXDURZVYL
      UATZWXFVYHVYJWXGVYLUAVYHWUCWULVYJWXGUCWUKWUQVVKVVLXCWEWGVYHWXDVALVYLVALWX
      HWXFUCVYHWXDVYHWUEWUMWXDUMLZWWFWUPVVKVVLWKWEZVDVYHVYLVYHVYTVYLUMLZWUAVYKX
      DVFZVDWXDVYLWOWEWPVYHUFWXEUIPZWXFUFUIPZVYHWXIUFWXDUIPZWXKUFVYLUIPZWXMWXJV
      XCVYDWXOVYEVXCWUEWUFWUMUFVVLUIPZWXOWUIVXCWUEWUFWUGWUHXEWUOVXCWXQWUGVXCWUE
      WUFWUGWUHXFVXCWUNWUEWXQWUGXGVIWUIRVVKXHVMXIVVKVVLXJVPVLWXLVYHVYTWXPWUAVYK
      XKVFWXDVYLXJVPVYHWXEUMLZWXMWXNXGVYHWXIWXKWXRWXJWXLWXDVYLWKWEWXEXLVFXMXNXN
      XOXPXQXRVXDVXTDEVVSVVSVVHVYBVWLVVSLZUJZVXCVXTVVHWXTUJWVBWVCUJZVWQVALZVWMV
      ALZUJZUJZWUCVXTVXCVVFVVGWXTWYEVVEVVFVVGUJZVYBWXSWYEWYFVYBUJWYAWYFWXSUJWYD
      VVFVVGVYBWYAVVFVYBUJWVBVVGVYBUJWVCBVWCFXSCVWCFXSXTYAVVFVVGWXSWYDVVFWXSUJW
      YBVVGWXSUJWYCBVWLFXSCVWLFXSXTYAXTYBYCWUJWYAWYDWUCVXTWYAWYDWUCMZWXDVYKVWMV
      WQSTZUATZUATZWXDVWQVWMSTZVWGVWDSTZUATZUATZVXQVXSWYGWYIWYMWXDUAWYGWYIWYHVY
      KUATZWYMWYGVYSWYHVALZWYIWYOUCWYAWYDVYSWUCVWDVWGYDVRZWYDWYAWYPWUCWYCWYBWYP
      VWMVWQYDYEVSZVYKWYHYFWEWYGWYCWYBWVBWVCWYOWYMUCWYAWYBWYCWUCYGZWYAWYBWYCWUC
      YHZWVBWVCWYDWUCYIZWVBWVCWYDWUCYJZVWMVWQVWDVWGYKVPWPVGWYGWYJVYPVVLWYHUATZU
      ATZVXQWYGWUCWULVYSWYPWYJXUDUCWYAWYDWUCYLZWUCWYAWULWYDWVMWUCWULWBRVVKYDYMV
      LZWYQWYRVVKVVLVYKWYHVOVPWYGVYPVXHXUCVXPUAWYGVYPWUSWVAVXHWYGWUCWVBWVCWVDXU
      EXUAXUBWVGWAWYGWURWUTVXFSWYGWVIWVKWURWUTWYGWULWVBWVLXUFXUAWVNWEWYGWVHVVKV
      WDUAWUCWYAWVOWYDWVMWUCWVOWBWVPYMVLZWGWYGWVJVWDVXESWYGWVBWVQXUAWVRVFWGWIWG
      WYGWVBWVSWVTWWAXUAWYGWULWVBWVSXUFXUAVVLVWDYNWEZWYGWUCWVCWVTXUEXUBVVKVWGYN
      WEZWWIWAWNWYGXUCVWMVXNSTZVXMSTZVWMVXMSTVXNSTZVXPWYGXUCVVLVWMUATZVXMSTZXUK
      WYGWULWYCWYBXUCXUNUCXUFWYSWYTVVLVWMVWQVTWAWYGXUMXUJVXMSWYGXUMRVWMUATZVXNS
      TZXUJWYGWUCWYCXUMXUPUCZXUEWYSWVMWUCWYCXUQWBRVVKVWMWCWDWEWYGXUOVWMVXNSWYGW
      YCXUOVWMUCWYSVWMWHVFWGWPWGWPWYGWYCVXNVALZVXMVALZXUKXULUCWYSWYGWUCWYCXURXU
      EWYSVVKVWMYNWEZWYGWULWYBXUSXUFWYTVVLVWQYNWEZVWMVXNVXMYOWAWYGWYCXUSXURXULV
      XPUCWYSXVAXUTVWMVXMVXNWMWAWNXAWPWYGWYNVVKWYKUATZVVLWYLUATZUATZVXSWYGWUCWU
      LWYKVALZWYLVALZWYNXVDUCXUEXUFWYDWYAXVEWUCVWQVWMYDVSWYAWYDXVFWUCWVCWVBXVFV
      WGVWDYDYEVRVVKVVLWYKWYLVOVPWYGXVBVXRXVCVXIUAWYGXVBVVKVWQUATZVXNSTZVWQVXMS
      TZVXNSTZVXRWYGWUCWYBWYCXVBXVHUCXUEWYTWYSVVKVWQVWMVTWAWYGXVIXVGVXNSWYGWVHV
      WQUATZRVWQUATZVXMSTZXVGXVIWYGWULWYBXVKXVMUCZXUFWYTWVMWULWYBXVNWBRVVLVWQWC
      WDWEWYGWVHVVKVWQUAXUGWGWYGXVLVWQVXMSWYGWYBXVLVWQUCWYTVWQWHVFWGYPWGWYGWYBX
      USXURXVJVXRUCWYTXVAXUTVWQVXMVXNWMWAYQWYGXVCWWNVXESTZWWPVXESTZVXIWYGWULWVC
      WVBXVCXVOUCXUFXUBXUAVVLVWGVWDVTWAWYGWWNWWPVXESWYGWWNWWSWWPWYGWUCWVCWWTXUE
      XUBWXAWEWYGWWRVWGVXFSWYGWVCWXBXUBWXCVFWGWPWGWYGXVPVWGVXESTVXFSTZVXIWYGWVC
      WVTWVSXVPXVQUCXUBXUIXUHVWGVXFVXEYOWAWYGWVCWVSWVTXVQVXIUCXUBXUHXUIVWGVXEVX
      FWMWAWPWNXAWPWIXOYRXQYSVVTVXBVXLVYAUJVVTVWKVXLVXAVYAVVTVWJVXKDVVSVVTVYBUJ
      ZVWEVXGUCZVWJVXKXGVVRXVSGVWCVVSGDYTZVVJVWEVVQVXGVVIVWCAUUAXVTVVNVXEVVPVXF
      UBXVTVVMVWDVVLUAVVIVWCBUUAVGXVTVVOVWGVVKUAVVIVWCCUUAVGXAUUBUUCZXVSVWIVXJU
      FUIXVSVWFVXHVWHVXIUAVWEVXGVWDSUUDZVWEVXGVWGSUUDZXAUUEVFUUFVVTVWTVXTDEVVSV
      VSVVTVYBWXSVWTVXTXGZXVRXVSVWNVXOUCZXWDVVTWXSUJXWAVVRXWEGVWLVVSGEYTZVVJVWN
      VVQVXOVVIVWLAUUAXWFVVNVXMVVPVXNUBXWFVVMVWQVVLUAVVIVWLBUUAVGXWFVVOVWMVVKUA
      VVIVWLCUUAVGXAUUBUUCXVSXWEUJVWPVXQVWSVXSXVSXWEVWFVXHVWOVXPUAXWBVWNVXOVWMS
      UUDUUGXWEXVSVWRVXRVWHVXIUAVWNVXOVWQSUUDXWCUUHUUBYRYBUUIUUJUUKWEUULVVHVXBV
      WDVWEUIPZVWEVWGUIPZUJZVWGVWEUIPZVWEVWDUIPZUJZUUMZDVVSUEZVXAUJZVWBVVHVWKXW
      NVXAVVHVWJXWMDVVSVYCVYDVWEUMLZVYEVWJXWMXGVYFVVEVVFVYBXWPVVGAVWCFUNUUNZVYG
      VWDVWEVWGUUOWAUUFUUPVVHXWOVWBULZBCVVHBCUCZXWOBAUCZVWBVVHXWSXWOXWTULVVHXWS
      UJZXWNXWTVXAXXAXWTXWNXXAXWTVWDVWEUCZDVVSUEZXWNXXAVVFVVEXWTXXCXGVVEVVFVVGX
      WSUUQVVEVVFVVGXWSUURBADFUUSWEXXAXXBXWMDVVSXXAVYBUJZXXBXWGXWKUJZXWMXXDVYDX
      WPXXBXXEXGVVHVYBVYDXWSVYFUUTVVHVYBXWPXWSXWQUUTVWDVWEUVAWEXXEXXEXXEUUMZXXD
      XWMXXEUVBXWSXXFXWMXGVVHVYBXWSXXEXWIXXEXWLXWSXWKXWHXWGXWSVWDVWGVWEUIVWCBCU
      VCZUVDUVEXWSXWGXWJXWKXWSVWDVWGVWEUIXXGUUEUUPUVFUVGUVHUVIUUFUVIUVJUVKUVLVV
      HXWSXWTVWBVVHXWSXWTUJZUJZUFVWALVVJRVVMUATZUFVVOUATZUBTZUCZGVVSUEZVWBUVMXX
      IXXMGVVSVVHVVIVVSLZXXHXXMVVHXXOUJZVVJVALZVVMVALZVVOVALZMZXXHXXMXXPXXQXXRX
      XSVVEVVFXXOXXQVVGAVVIFXSZUUNVVFVVEXXOXXRVVGBVVIFXSZUOVVGVVEXXOXXSVVFCVVIF
      XSZUPUVNXXTXXHUJXXLVVMVVJXXTXXLVVMUCZXXHXXRXXSXYDXXQXXRXXSUJXXLVVMUFUBTZV
      VMXXRXXSXXJVVMXXKUFUBVVMWHVVOUVOZUUGXXRXYEVVMUCXXSVVMUVPUVQWPUVRUVQXWTVVM
      VVJUCXXTXWSVVIBAUVCUVSUVTXPXQXRVVTXXNHUFVWAVVKUFUCZVVRXXMGVVSXYGVVQXXLVVJ
      XYGVVNXXJVVPXXKUBXYGVVLRVVMUAXYGVVLRUFSTRVVKUFRSUUDRWBUWAUWBWGVVKUFVVOUAU
      WCXAUWDUWEUWFVMUWGUWHVVHBCUWIZIQZBKZXYICKZUWIZIVVSUHZXWRVVHXYHXYJXYKUCZIV
      VSUEZUWJZXYMVVHXYOBCVVFVVGXWSXYOXGVVEBCIFUUSUVRUWKXYMXYNUWJZIVVSUHXYPXYLX
      YQIVVSXYJXYKUWLUWMXYNIVVSUWNUWOUWPVVHXYLXWRIVVSVVHXYIVVSLZXYLXWRVVHXYRXYL
      UJZUJZXWOXYIAKZXYJSTZXYKXYJSTZUWQTZVWALZVVJRYUDSTZVVMUATZYUDVVOUATZUBTZUC
      ZGVVSUEZUJVWBXYTXWNYUEVXAYUKXYTXWNXYJYUAUIPZYUAXYKUIPZUJZXYKYUAUIPZYUAXYJ
      UIPZUJZUUMZYUEXYRXWNYURULVVHXYLXWMYURDXYIVVSDIYTZXWIYUNXWLYUQYUSXWGYULXWH
      YUMYUSVWDXYJVWEYUAUIVWCXYIBUUAZVWCXYIAUUAZUWRYUSVWEYUAVWGXYKUIYVAVWCXYICU
      UAZUWRUUJYUSXWJYUOXWKYUPYUSVWGXYKVWEYUAUIYVBYVAUWRYUSVWEYUAVWDXYJUIYVAYUT
      UWRUUJUVFUWSUWTXYTYURYUEXYTYUNYUEYUQXYTYUNUJZYUEYUBYUCUIPZYVCYUMYVDXYTYUL
      YUMUXAXYTYUMYVDXGZYUNXYTYUAUMLZXYKUMLZXYJUMLZYVEVVHVVEXYRYVFXYSVVEVVFVVGW
      JXYRXYLUXBZAXYIFUNYRZVVHVVGXYRYVGXYSVVEVVFVVGYLYVICXYIFUNYRZXYTVVFXYRYVHV
      VEVVFVVGXYSUUQVVHXYRXYLUXCBXYIFUNWEZYUAXYKXYJUXDWAUVQXMYVCYUBUMLZUFYUBUIP
      ZYUCUMLZUFYUCUXEPZYUEYVDXGXYTYVMYUNXYTYVFYVHYVMYVJYVLYUAXYJVBWEUVQYVCYVNY
      ULXYTYULYUMUXCXYTYVNYULXGZYUNXYTYVFYVHYVQYVJYVLYUAXYJXHWEUVQXIXYTYVOYUNXY
      TYVGYVHYVOYVKYVLXYKXYJVBWEUVQYVCXYJXYKUXEPZYVPYVCYVRXYJXYKUIPZXYKXYJUWIZX
      YTYVRYVSYVTUJXGZYUNXYTYVHYVGYWAYVLYVKXYJXYKUXFWEUVQXYTYUNYVSXYTYVHYVFYVGY
      UNYVSULYVLYVJYVKXYJYUAXYKUXGWAUXHYVCXYJXYKVVHXYRXYLYUNUXIUXJUXKXYTYVRYVPX
      GZYUNXYTYVHYVGYWBYVLYVKXYJXYKUXLWEUVQXMYUBYUCUXMVPXIXYTYUQUJZYUDXYJYUASTZ
      XYJXYKSTZUWQTZVWAXYTYUDYWFUCZYUQXYTYUAVALZXYJVALZXYKVALZYWIYVTYWGXYTYUAYV
      JVDZXYTXYJYVLVDZXYTXYKYVKVDZYWLXYTXYJXYKVVHXYRXYLUXAUXJYUAXYJXYKXYJUXNUXO
      UVQYWCYWFVWALZYWDYWEUIPZYWCYUOYWOXYTYUOYUPUXCXYTYUOYWOXGZYUQXYTYVGYVFYVHY
      WPYVKYVJYVLXYKYUAXYJUXPWAUVQXMYWCYWDUMLZUFYWDUIPZYWEUMLZUFYWEUXEPZYWNYWOX
      GXYTYWQYUQXYTYVHYVFYWQYVLYVJXYJYUAVBWEUVQYWCYWRYUPXYTYUOYUPUXAXYTYWRYUPXG
      ZYUQXYTYVHYVFYXAYVLYVJXYJYUAXHWEUVQXIXYTYWSYUQXYTYVHYVGYWSYVLYVKXYJXYKVBW
      EUVQYWCXYKXYJUXEPZYWTYWCYXBXYKXYJUIPZXYLXYTYXBYXCXYLUJXGZYUQXYTYVGYVHYXDY
      VKYVLXYKXYJUXFWEUVQXYTYUQYXCXYTYVGYVFYVHYUQYXCULYVKYVJYVLXYKYUAXYJUXGWAUX
      HVVHXYRXYLYUQUXIUXKXYTYXBYWTXGZYUQXYTYVGYVHYXEYVKYVLXYKXYJUXLWEUVQXMYWDYW
      EUXMVPXIUXQUXRUVLUXSXYTVXAYUJGVVSXYTXXOVXAYUJVVHXYSXXOVXAYUJULVVHXYSXXOMZ
      VXAYWDVVOVVJSTZUATZVVMVVJSTZXYKYUASTZUATZUCZYUJYXFXYRXXOVXAYXLULVVHXYRXYL
      XXOYHVVHXYSXXOYLZVWTYXLYWDVWOUATZVWRYXJUATZUCDEXYIVVIVVSVVSYUSVWPYXNVWSYX
      OYUSVWFYWDVWOUAYUSVWDXYJVWEYUASYUTYVAXAWGYUSVWHYXJVWRUAYUSVWGXYKVWEYUASYV
      BYVAXAVGUUBEGYTZYXNYXHYXOYXKYXPVWOYXGYWDUAYXPVWMVVOVWNVVJSVWLVVICUUAVWLVV
      IAUUAZXAVGYXPVWRYXIYXJUAYXPVWQVVMVWNVVJSVWLVVIBUUAYXQXAWGUUBUXTWEYXFXXQXX
      RXXSYWHYWIYWJYVTYXLYUJULVVEXXOXXQYXFXYAVVEVVFVVGXYSXXOUYAYXMUYBVVFXXOXXRY
      XFXYBVVEVVFVVGXYSXXOUYCYXMUYBVVGXXOXXSYXFXYCVVEVVFVVGXYSXXOUYDYXMUYBVVHXY
      SYWHXXOYWKVCVVHXYSYWIXXOYWLVCVVHXYSYWJXXOYWMVCYXFXYJXYKVVHXYRXYLXXOYGUXJX
      XTYWHYWIYWJMZYVTMZYXLYUJYXSYXLUJZYXJVVMUATZYUBVVOUATZUBTZYUCUWQTZYYAYUCUW
      QTZYYBYUCUWQTZUBTZVVJYUIYXTYYAVALZYYBVALZYUCVALZYUCUFUWIZYYDYYGUCYXTYXJVA
      LZXXRYYHYXTYWJYWHYYLYWHYWIYWJXXTYVTYXLUYEZYWHYWIYWJXXTYVTYXLUYFZXYKYUAYDW
      EZXXQXXRXXSYXRYVTYXLUYGZYXJVVMYNWEZYXTYUBVALZXXSYYIYXTYWHYWIYYRYYNYWHYWIY
      WJXXTYVTYXLUYHZYUAXYJYDWEZXXQXXRXXSYXRYVTYXLUYIZYUBVVOYNWEZYXTYWJYWIYYJYY
      MYYSXYKXYJYDWEZYXTYYKYVTXXTYXRYVTYXLUYJYXTYUCUFXYKXYJYXTYWJYWIYUCUFUCXYKX
      YJUCXGYYMYYSXYKXYJUYKWEUYLXIZYYAYYBYUCUYMUYNYXTYYDVVJUCZYUCVVJUATZYYCUCZY
      XTUUUFYXHYXJVVJUATZUBTZYYBUBTZYXKUUUHUBTZYYBUBTYYCYXTUUUFYWDVVOUATZUUUFUB
      TZYYBUBTZUUUJYXTUUULYYBUBTZUUUFUBTZUFUUUFUBTZUUUNUUUFYXTUUUOUFUUUFUBYXTYW
      DYUBUBTZVVOUATZXXKUUUOUFYXTUUURUFVVOUAYXTYWIYWHUUURUFUCYYSYYNXYJYUAUYOWEW
      GYXTYWDVALZYYRXXSUUUSUUUOUCYXTYWIYWHUUUTYYSYYNXYJYUAYDWEZYYTUUUAYWDYUBVVO
      UYPWAYXTXXSXXKUFUCUUUAXYFVFWIWGYXTUUULVALZYYIUUUFVALZUUUPUUUNUCYXTUUUTXXS
      UUVBUUVAUUUAYWDVVOYNWEZUUUBYXTYYJXXQUUVCUUUCXXQXXRXXSYXRYVTYXLUYQZYUCVVJY
      NWEZUUULYYBUUUFUYRWAYXTUUVCUUUQUUUFUCUUVFUUUFUYSVFYPYXTUUUMUUUIYYBUBYXTUU
      ULUUUHUBTYWDVVJUATZSTZUUULUUVGSTZUUUHUBTZUUUMUUUIYXTUUVBUUUHVALZUUVGVALZU
      UVHUUVJUCUUVDYXTYYLXXQUUVKYYOUUVEYXJVVJYNWEZYXTUUUTXXQUUVLUUVAUUVEYWDVVJY
      NWEZUUULUUUHUUVGUYTWAYXTUUUMUUULUUUHUUVGSTZUBTZUUVHYXTUUUFUUVOUUULUBYXTYX
      JYWDSTZVVJUATZUUUFUUVOYXTUUVQYUCVVJUAYXTYWJYWIYWHUUVQYUCUCYYMYYSYYNXYKXYJ
      YUAVUAWAWGYXTYYLUUUTXXQUUVRUUVOUCYYOUUVAUUVEYXJYWDVVJWCWAVUBVGYXTUUVBUUVK
      UUVLUUVHUUVPUCUUVDUUVMUUVNUUULUUUHUUVGVUCWAVUDYXTYXHUUVIUUUHUBYXTUUUTXXSX
      XQYXHUUVIUCUUVAUUUAUUVEYWDVVOVVJVTWAWGVUEWGWPYXTUUUIUUUKYYBUBYXTYXHYXKUUU
      HUBYXSYXLVUFWGWGYXTUUUKYYAYYBUBYXTUUUKYXJYXIUATZUUUHUBTZYXJYXIVVJUBTZUATZ
      YYAYXTYXKUUVSUUUHUBYXTYXIVALZYYLYXKUUVSUCYXTXXRXXQUUWCYYPUUVEVVMVVJYDWEZY
      YOYXIYXJYFWEWGYXTYYLUUWCXXQUUWBUUVTUCYYOUUWDUUVEYXJYXIVVJVUGWAYXTUUWAVVMY
      XJUAYXTXXRXXQUUWAVVMUCYYPUUVEVVMVVJVUHWEVGYQWGWNYXTYYCVALZXXQYYJYYKUUUEUU
      UGXGYXTYYHYYIUUWEYYQUUUBYYAYYBVUIWEUUVEUUUCUUUDYYCVVJYUCVUJUYNXIYXTYYEYUG
      YYFYUHUBYXTYYEYXJYUCUWQTZVVMUATZYUGYXTYYLXXRYYJYYKYYEUUWGUCYYOYYPUUUCUUUD
      YXJVVMYUCVUKUYNYXTUUWFYUFVVMUAYXTYUCYUBSTZYUCUWQTZYUCYUCUWQTZYUDSTZUUWFYU
      FYXTYYJYYRYYJYYKUUWIUUWKUCUUUCYYTUUUCUUUDYUCYUBYUCVULUYNYXTUUWHYXJYUCUWQY
      XTYWJYWHYWIUUWHYXJUCYYMYYNYYSXYKYUAXYJVUAWAWGYXTUUWJRYUDSYXTYYJYYKUUWJRUC
      UUUCUUUDYUCVUMWEWGWIWGWPYXTYYRXXSYYJYYKYYFYUHUCYYTUUUAUUUCUUUDYUBVVOYUCVU
      KUYNXAWIUVLVUNUXSVUOVUPVUQVURVVTYUKHYUDVWAVVKYUDUCZVVRYUJGVVSUUWLVVQYUIVV
      JUUWLVVNYUGVVPYUHUBUUWLVVLYUFVVMUAVVKYUDRSUUDWGVVKYUDVVOUAUWCXAUWDUWEUWFV
      USUWGVUTVVAVVBVVAVVCUVI $.
      $( [24-Jun-2013] $)
  $}


  $( Lemma for ~ colinearalg .  Expand out a multiplication. $)
  colinearalglem1 $p |- (
    ( ( A e. CC /\ B e. CC /\ C e. CC ) /\
      ( D e. CC /\ E e. CC /\ F e. CC ) ) ->
    ( ( ( B - A ) x. ( F - D ) ) = ( ( E - D ) x. ( C - A ) ) <->
      ( ( B x. F ) - ( ( A x. F ) + ( B x. D ) ) ) =
      ( ( C x. E ) - ( ( A x. E ) + ( C x. D ) ) ) ) ) $=
    ( cc wcel w3a wa cmin co cmul wceq caddc simpl2 simpl1 subcl syl11anc
    simpr3 simpr1 subdi syl111anc simp2 simp3 mulcl syl2an simp1 addsub subdir
    opreq12d subsub3 eqtr2d subsub4 opreq1d 3eqtr3d eqtrd simpr2 simpl3 mulcom
    3eqtrd eqeq12d wb addcl addcan2 bitrd ) AGHZBGHZCGHZIZDGHZEGHZFGHZIZJZBAKLZ
    FDKLMLZEDKLZCAKLZMLZNBFMLZAFMLZBDMLZOLZKLZADMLZOLZCEMLZAEMLZCDMLZOLZKLZWFOL
    ZNZWEWLNZVOVQWGVTWMVOVQVPFMLZVPDMLZKLZWGVOVPGHZVMVKVQWRNVOVHVGWSVGVHVIVNPZV
    GVHVIVNQZBARSVJVKVLVMTZVJVKVLVMUAZVPFDUBUCVOWAWBKLZWFOLWCKLZXDWCKLZWFOLZWRW
    GVOXDGHZWFGHZWCGHZXEXGNVOWAGHZWBGHZXHVJVHVMXKVNVGVHVIUDZVKVLVMUEZBFUFUGZVJV
    GVMXLVNVGVHVIUHZXNAFUFUGZWAWBRSZVJVGVKXIVNXPVKVLVMUHZADUFUGZVJVHVKXJVNXMXSB
    DUFUGZXDWFWCUIUCVOWRXDWCWFKLZKLZXEVOWPXDWQYBKVOVHVGVMWPXDNWTXAXBBAFUJUCVOVH
    VGVKWQYBNWTXAXCBADUJUCUKVOXHXJXIYCXENXRYAXTXDWCWFULUCUMVOXFWEWFOVOXKXLXJXFW
    ENXOXQYAWAWBWCUNUCUOUPUQVOVTVSVRMLZVSEMLZVSDMLZKLZWMVOVRGHZVSGHZVTYDNVOVLVK
    YHVJVKVLVMURZXCEDRSVOVIVGYIVGVHVIVNUSZXACARSZVRVSUTSVOYIVLVKYDYGNYLYJXCVSED
    UBUCVOWHWIKLZWFOLWJKLZYMWJKLZWFOLZYGWMVOYMGHZXIWJGHZYNYPNVOWHGHZWIGHZYQVJVI
    VLYSVNVGVHVIUEZVKVLVMUDZCEUFUGZVJVGVLYTVNXPUUBAEUFUGZWHWIRSZXTVJVIVKYRVNUUA
    XSCDUFUGZYMWFWJUIUCVOYGYMWJWFKLZKLZYNVOYEYMYFUUGKVOVIVGVLYEYMNYKXAYJCAEUJUC
    VOVIVGVKYFUUGNYKXAXCCADUJUCUKVOYQYRXIUUHYNNUUEUUFXTYMWJWFULUCUMVOYOWLWFOVOY
    SYTYRYOWLNUUCUUDUUFWHWIWJUNUCUOUPVAVBVOWEGHZWLGHZXIWNWOVCVOXKWDGHZUUIXOVOXL
    XJUUKXQYAWBWCVDSWAWDRSVOYSWKGHZUUJUUCVOYTYRUULUUDUUFWIWJVDSWHWKRSXTWEWLWFVE
    UCVF $.
    $( [24-Jun-2013] $)

  ${
    $d A i j $.  $d B i j $.  $d C i j $.  $d N i j $.
    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem2 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( C ` i ) - ( B ` i ) ) x. ( ( A ` j ) - ( B ` j ) ) ) =
         ( ( ( C ` j ) - ( B ` j ) ) x. ( ( A ` i ) - ( B ` i ) ) ) ) ) $=
      ( cee cfv wcel w3a cv cmin co cmul wceq c1 cfz wa cc wb simp1 simpl
      fveecn syl2an simp2 simp3 simpr caddc mulcl addcl syl11anc subsub4
      syl111anc subsub3 eqcomd opreq2d subcl subsub eqtrd 3eqtr2d addsub12
      3eqtrd eqeq1d subadd2 eqcom syl6bb addsub addcom opreq1d eqtr3d eqeq2d
      subeqrev syl22anc 3bitrd subadd syl5rbbr sub23 bitrd 3bitr3d
      colinearalglem1 3anrot syl2anb 3bitr4d syl33anc 2ralbidva ) AFGHZIZBWFIZC
      WFIZJZDKZBHZWKAHZLMEKZCHZWNAHZLMNMWNBHZWPLMWKCHZWMLMNMOZWRWLLMWPWQLMNMWOW
      QLMWMWLLMNMOZDEPFQMZXAWJWKXAIZWNXAIZRZRWMSIZWLSIZWRSIZWPSIZWQSIZWOSIZWSWT
      TWJWGXBXEXDWGWHWIUAZXBXCUBZAWKFUCUDWJWHXBXFXDWGWHWIUEZXLBWKFUCUDWJWIXBXGX
      DWGWHWIUFZXLCWKFUCUDWJWGXCXHXDXKXBXCUGZAWNFUCUDWJWHXCXIXDXMXOBWNFUCUDWJWI
      XCXJXDXNXOCWNFUCUDXEXFXGJZXHXIXJJZRZWLWONMZWMWONMZWLWPNMZUHMZLMWRWQNMZWMW
      QNMZWRWPNMZUHMZLMOZYEYAYCUHMLMZXTXSYDUHMZLMZOZWSWTXRYBXSLMZYDLMZYEYCLMZOZ
      YAYJUHMZYNOZYGYKXRYMYPYNXRYMXTXSLMZYAYDLMZUHMZYAYRYDLMZUHMZYPXRYMYBYILMZX
      TYIYALMZLMZYTXRYBSIZXSSIZYDSIZYMUUCOXRXTSIZYASIZUUFXPXEXJUUIXQXEXFXGUAZXH
      XIXJUFZWMWOUIUDZXPXFXHUUJXQXEXFXGUEZXHXIXJUAZWLWPUIUDZXTYAUJUKZXPXFXJUUGX
      QUUNUULWLWOUIUDZXPXEXIUUHXQUUKXHXIXJUEZWMWQUIUDZYBXSYDULUMXRUUIYISIZUUJUU
      EUUCOUUMXRUUGUUHUVAUURUUTXSYDUJUKZUUPXTYIYAUNUMXRUUEXTXSYSLMZLMZYTXRUUDUV
      CXTLXRUVCUUDXRUUGUUJUUHUVCUUDOUURUUPUUTXSYAYDUNUMUOUPXRUUIUUGYSSIZUVDYTOU
      UMUURXRUUJUUHUVEUUPUUTYAYDUQUKXTXSYSURUMUSUTXRYRSIZUUJUUHYTUUBOXRUUIUUGUV
      FUUMUURXTXSUQUKUUPUUTYRYAYDVAUMXRUUAYJYAUHXRUUIUUGUUHUUAYJOUUMUURUUTXTXSY
      DULUMUPVBVCXRYOYLYNYDUHMZOZYLYFYCLMZOZYGXRYOUVGYLOZUVHXRYLSIZUUHYNSIZYOUV
      KTXRUUFUUGUVLUUQUURYBXSUQUKUUTXRYESIZYCSIZUVMXPXGXHUVNXQXEXFXGUFZUUOWRWPU
      IUDZXPXGXIUVOXQUVPUUSWRWQUIUDZYEYCUQUKZYLYDYNVDUMUVGYLVEVFXRUVGUVIYLXRYEY
      DUHMZYCLMZUVGUVIXRUVNUUHUVOUWAUVGOUVQUUTUVRYEYDYCVGUMXRUVTYFYCLXRUVNUUHUV
      TYFOUVQUUTYEYDVHUKVIVJVKXRUUFUUGYFSIZUVOUVJYGTUUQUURXRUUHUVNUWBUUTUVQYDYE
      UJUKUVRYBXSYFYCVLVMVNXRYQYJYHOZYKXRYQYJYNYALMZOZUWCUWEUWDYJOZXRYQUWDYJVEX
      RUVMUUJYJSIZUWFYQTUVSUUPXRUUIUVAUWGUUMUVBXTYIUQUKYNYAYJVOUMVPXRUWDYHYJXRU
      WDYEYALMYCLMZYHXRUVNUVOUUJUWDUWHOUVQUVRUUPYEYCYAVQUMXRUVNUUJUVOUWHYHOUVQU
      UPUVRYEYAYCULUMUSVKVRYJYHVEVFVSWMWLWRWPWQWOVTXPXFXGXEJXIXJXHJWTYKTXQXEXFX
      GWAXHXIXJWAWLWRWMWQWOWPVTWBWCWDWE $.
      $( [24-Jun-2013] $)

    $( Lemma for ~ colinearalg .  Translate between two forms of the
       colinearity condition. $)
    colinearalglem3 $p |- (
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
       ( A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
         ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) )
         <->
         A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
         ( ( ( A ` i ) - ( C ` i ) ) x. ( ( B ` j ) - ( C ` j ) ) ) =
         ( ( ( A ` j ) - ( C ` j ) ) x. ( ( B ` i ) - ( C ` i ) ) ) ) ) $=
      ( cee cfv wcel w3a cv cmin co cmul wceq c1 cfz wral colinearalglem2 wb
      3comr bitrd ) AFGHZIZBUCIZCUCIZJDKZBHZUGAHZLMEKZCHZUJAHZLMNMUJBHZULLMUGCH
      ZUILMNMOEPFQMZRDUORUNUHLMULUMLMNMUKUMLMUIUHLMNMOEUORDUORZUIUNLMUMUKLMNMUL
      UKLMUHUNLMNMOEUORDUORZABCDEFSUEUFUDUPUQTBCADEFSUAUB $.
      $( [24-Jun-2013] $)


  $}

  ${
    $d A i $.  $d C i $.  $d K i $.  $d N i $.
    $( Lemma for ~ colinearalg .  Prove a disjunction that will be needed in
       the final proof. $)
    colinearalglem4 $p |- (
       ( ( A e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\ K e. RR ) ->
       ( A. i e. ( 1 ... N ) ( ( ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) +
         ( A ` i ) ) - ( A ` i ) ) x. ( ( C ` i ) - ( A ` i ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( C ` i ) - ( ( K x. ( ( C ` i ) -
         ( A ` i ) ) ) + ( A ` i ) ) ) x. ( ( A ` i ) -
         ( ( K x. ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) ) ) <_ 0 \/
         A. i e. ( 1 ... N ) ( ( ( A ` i ) - ( C ` i ) ) x. ( ( ( K x.
         ( ( C ` i ) - ( A ` i ) ) ) + ( A ` i ) ) - ( C ` i ) ) ) <_ 0 ) ) $=
      ( cee cfv wcel wa cr cc0 cle wbr c1 w3o cv cmin co cmul caddc cfz wral
      relin01 adantl fveere adantlr adantll jca c2 cexp cc wceq simprl recnd
      resubcl ancoms adantr mulass syl111anc remulcl syl11anc recn ad2antrr
      pncan opreq1d sqval syl opreq2d 3eqtr4d wo simprr sqge0 orcd wb resqcl
      mulle0b mpbird eqbrtrd sylan an32s r19.21aiva expr cneg ad2antlr sub23
      ax-1cn subdir mp3an1 mulid2 eqtr2d subsub4 3eqtr3rd subid df-neg syl6eqr
      opreq12d 1re mpan ad2antrl mulneg2 mul4 syl22anc negeqd 3eqtrd simpl
      sylancr subge0 biimpar adantrl mulge0 breqtrd le0neg2 mpbid negsubdi2
      simplr simpll peano2rem mulneg1 mul12 eqtr4d eqtrd mp3an2 subsub3 mpan2
      3orim123d mpd ) AEFGZHZBYGHZIZDJHZIZDKLMZKDLMZDNLMZIZNDLMZOZDCPZBGZYSAGZQ
      RZSRZUUATRZUUAQRZUUBSRZKLMZCNEUARZUBZYTUUDQRZUUAUUDQRZSRZKLMZCUUHUBZUUAYT
      QRZUUDYTQRZSRZKLMZCUUHUBZOYKYRYJDUCUDYLYMUUIYPUUNYQUUSYJYKYMUUIYJYKYMIZIU
      UGCUUHYJYSUUHHZUUTUUGYJUVAIZUUAJHZYTJHZIZUUTUUGUVBUVCUVDYHUVAUVCYIAYSEUEU
      FYIUVAUVDYHBYSEUEUGUHZUVEUUTIZUUFDUUBUIUJRZSRZKLUVGUUCUUBSRZDUUBUUBSRZSRZ
      UUFUVIUVGDUKHZUUBUKHZUVNUVJUVLULUVGDUVEYKYMUMZUNUVGUUBUVEUUBJHZUUTUVDUVCU
      VPYTUUAUOZUPZUQZUNZUVTDUUBUUBURUSUVGUUEUUCUUBSUVGUUCUKHZUUAUKHZUUEUUCULUV
      GUUCUVGYKUVPUUCJHZUVOUVSDUUBUTZVAUNUVCUWBUVDUUTUUAVBZVCUUCUUAVDVAVEUVGUVH
      UVKDSUVGUVNUVHUVKULZUVTUUBVFZVGVHVIUVGUVIKLMZYMKUVHLMZIZYNUVHKLMIZVJZUVGU
      WJUWKUVGYMUWIUVEYKYMVKUVGUVPUWIUVSUUBVLZVGUHVMUVGYKUVHJHZUWHUWLVNUVOUVGUV
      PUWNUVSUUBVOZVGDUVHVPVAVQVRVSVTWAWBYJYKYPUUNYJYKYPIZIUUMCUUHYJUVAUWPUUMUV
      BUVEUWPUUMUVFUVEUWPIZUULNDQRZDSRZUVKSRZWCZKLUWQUULUWRUUBSRZUUCWCZSRZUXBUU
      CSRZWCZUXAUWQUUJUXBUUKUXCSUWQUUBUUCQRZYTUUCQRUUAQRZUXBUUJUWQYTUKHZUWBUWAU
      XGUXHULUVDUXIUVCUWPYTVBZWDZUVCUWBUVDUWPUWEVCZUWQUUCUWQYKUVPUWCUVEYKYPUMZU
      VEUVPUWPUVRUQZUWDVAUNZYTUUAUUCWEUSUWQUXBNUUBSRZUUCQRZUXGUWQUVMUVNUXBUXQUL
      ZUWQDUXMUNZUWQUUBUXNUNZNUKHZUVMUVNUXRWFNDUUBWGWHVAUWQUXPUUBUUCQUWQUVNUXPU
      UBULZUXTUUBWIZVGVEWJUWQUXIUWAUWBUXHUUJULUXKUXOUXLYTUUCUUAWKUSWLUWQUUAUUAQ
      RZUUCQRZUUAUUCQRUUAQRZUXCUUKUWQUWBUWBUWAUYEUYFULUXLUXLUXOUUAUUAUUCWEUSUWQ
      UYEKUUCQRUXCUWQUYDKUUCQUWQUWBUYDKULUXLUUAWMVGVEUUCWNWOUWQUWBUWAUWBUYFUUKU
      LUXLUXOUXLUUAUUCUUAWKUSWLWPUWQUXBUKHUWAUXDUXFULUWQUXBUWQUWRJHZUVPUXBJHYKU
      YGUVEYPNJHZYKUYGWQNDUOZWRWSZUXNUWRUUBUTVAUNUXOUXBUUCWTVAUWQUXEUWTUWQUWRUK
      HUVNUVMUVNUXEUWTULUWQUWRUYJUNUXTUXSUXTUWRUUBDUUBXAXBXCXDUWQKUWTLMZUXAKLMZ
      UWQKUWSUVHSRZUWTLUWQUWSJHZKUWSLMZUWNUWIKUYMLMUWQUYGYKUYNUYJUXMUWRDUTVAZUW
      PUYOUVEUWPUYGKUWRLMZYKYNUYOUWPUYHYKUYGWQYKYPXEZUYIXFYKYOUYQYNYKUYQYOUYHYK
      UYQYOVNWQNDXGWRXHXIUYRYKYNYOUMUWRDXJXBUDUWQUVPUWNUXNUWOVGUWQUVPUWIUXNUWMV
      GUWSUVHXJXBUWQUVHUVKUWSSUWQUVNUWFUXTUWGVGVHXKUWQUWTJHZUYKUYLVNUWQUYNUVKJH
      ZUYSUYPUWQUVPUVPUYTUXNUXNUUBUUBUTVAUWSUVKUTVAUWTXLVGXMVRVSVTWAWBYJYKYQUUS
      YJYKYQIZIUURCUUHYJUVAVUAUURUVBUVEVUAUURUVFUVEVUAIZUUQDNQRZUVHSRZWCZKLVUBU
      UBWCZVUCUUBSRZSRZUUOVUGSRVUEUUQVUBVUFUUOVUGSVUBUXIUWBVUFUUOULUVDUXIUVCVUA
      UXJWDZUVCUWBUVDVUAUWEVCZYTUUAXNVAVEVUBVUHUUBVUGSRZWCZVUEVUBUVNVUGUKHVUHVU
      LULVUBUUBVUBUVDUVCUVPUVCUVDVUAXOUVCUVDVUAXPUVQVAZUNZVUBVUGVUBVUCJHZUVPVUG
      JHYKVUOUVEYQDXQWSZVUMVUCUUBUTVAUNUUBVUGXRVAVUBVUKVUDVUBVUKVUCUVKSRZVUDVUB
      UVNVUCUKHUVNVUKVUQULVUNVUBVUCVUPUNVUNUUBVUCUUBXSUSVUBUVHUVKVUCSVUBUVNUWFV
      UNUWGVGVHXTXCYAVUBVUGUUPUUOSVUBVUGUUCUXPQRZUUCUUBQRZUUPVUBUVMUVNVUGVURULZ
      VUBDUVEYKYQUMZUNVUNUVMUYAUVNVUTWFDNUUBWGYBVAVUBUXPUUBUUCQVUBUVNUYBVUNUYCV
      GVHVUBUWAUXIUWBVUSUUPULVUBUUCVUBYKUVPUWCVVAVUMUWDVAUNVUIVUJUUCYTUUAYCUSXD
      VHWLVUBKVUDLMZVUEKLMZVUBVUOKVUCLMZUWNUWIVVBVUPVUBVVDYQUVEYKYQVKYKVVDYQVNZ
      UVEYQYKUYHVVEWQDNXGYDWSVQVUBUVPUWNVUMUWOVGZVUBUVPUWIVUMUWMVGVUCUVHXJXBVUB
      VUDJHZVVBVVCVNVUBVUOUWNVVGVUPVVFVUCUVHUTVAVUDXLVGXMVRVSVTWAWBYEYF $.
      $( [27-Jun-2013] $)
  $}

  ${
    $d N i j k p t $.  $d A i j k p t $.  $d B i j k p t $.  $d C i j k p t $.
    $( An algebraic characterization of colinearity.  Note the similarity to
       ~ brbtwn2 .  (Contributed by Scott Fenton, 24-Jun-2013.) $)
    colinearalg $p |-
        (
          ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ->
          ( ( A Btwn <. B , C >. \/ B Btwn <. C , A >. \/
              C Btwn <. A , B >. ) <->
            A. i e. ( 1 ... N ) A. j e. ( 1 ... N )
            ( ( ( B ` i ) - ( A ` i ) ) x. ( ( C ` j ) - ( A ` j ) ) ) =
            ( ( ( B ` j ) - ( A ` j ) ) x. ( ( C ` i ) - ( A ` i ) ) ) ) ) $=
      ( vp cee cfv wcel w3a cop cbtwn wbr w3o cv cmin co cmul cc0 cle c1 cfz
      wral wceq wa brbtwn2 wb 3comr colinearalglem3 anbi2d bitrd
      colinearalglem2 3coml 3orbi123d wi cc fveecn subid opreq2d adantl subcl
      mul01 syl eqtrd syl2an anandirs 0re leidi syl6eqbr r19.21aiva 3adant1
      fveq1 opreq12d breq1d ralbidv syl5ibcom 3mix1 syl6 a1dd wne wrex wn
      simp3 simp1 eqeefv syl11anc necon3abid df-ne rexbii rexnal bitr2i syl6bb
      ralcom weq fveq2 opreq1d eqeq12d rcla4v ad2antrl cr fveere 3ad2antl1
      3ad2antl2 3ad2antl3 3jca anim1i anasss cdiv caddc adantlr recn 3anim123i
      adantr ad2antlr simplrr eqcom simp12 simp11 simp22 simp21 simp23 simpr3
      simpr1 subeq0 necon3bid biimp3ar divcl syl111anc simp13 mulcl subadd2
      bicomd div23 syl112anc eqeq2d divmul mulcom eqeq1d syl5bb 3bitr2d
      ralbidva 3simpb simpl2 simpl1 resubcl simpl3 recnd 3ad2ant1 biimpar
      redivcl colinearalglem4 opreq1 ralimi ralbi opreq2 syl5ibrcom sylbird
      syldan syld syl5bi exp32 r19.23adv sylbid pm2.61dne pm4.71rd wo andir
      orbi1i df-3or anbi1i bitri 3bitr4i syl6rbb ) AFHIZJZBUVOJZCUVOJZKZABCLMNZ
      BCALMNZCABLMNZODPZBIZUWCAIZQRZUWCCIZUWEQRZSRZTUANZDUBFUCRZUDZUWFEPZCIZUWM
      AIZQRZSRZUWMBIZUWOQRZUWHSRZUEZEUWKUDDUWKUDZUFZUWGUWDQRZUWEUWDQRZSRZTUANZD
      UWKUDZUXBUFZUWEUWGQRZUWDUWGQRZSRZTUANZDUWKUDZUXBUFZOZUXBUVSUVTUXCUWAUXIUW
      BUXOABCDEFUGUVSUWAUXHUXDUWOUWRQRSRUWNUWRQRUXESRUEEUWKUDDUWKUDZUFZUXIUVQUV
      RUVPUWAUXRUHBCADEFUGUIUVSUXQUXBUXHUVQUVRUVPUXQUXBUHBCADEFUJUIUKULUVRUVPUV
      QUWBUXOUHUVRUVPUVQKZUWBUXNUXJUWRUWNQRSRUWOUWNQRUXKSRUEEUWKUDDUWKUDZUFUXOC
      ABDEFUGUXSUXTUXBUXNCABDEFUMUKULUNUOUVSUXBUWLUXHUXNOZUXBUFZUXPUVSUXBUYAUVS
      UXBUYAUPZCAUVSCAUEZUYAUXBUVSUYDUWLUYAUVSUXKUWGUWGQRZSRZTUANZDUWKUDZUYDUWL
      UVQUVRUYHUVPUVQUVRUFZUYGDUWKUYIUWCUWKJZUFUYFTTUAUVQUVRUYJUYFTUEZUVQUYJUFU
      WDUQJZUWGUQJZUYKUVRUYJUFBUWCFURZCUWCFURZUYLUYMUFZUYFUXKTSRZTUYMUYFUYQUEUY
      LUYMUYETUXKSUWGUSUTVAUYPUXKUQJUYQTUEUWDUWGVBUXKVCVDVEVFVGTVHVIVJVKVLUYDUY
      GUWJDUWKUYDUYFUWITUAUYDUXKUWFUYEUWHSUYDUWGUWEUWDQUWCCAVMZUTUYDUWGUWEUWGQU
      YRUTVNVOVPVQUWLUXHUXNVRVSVTUVSCAWAZGPZCIZUYTAIZWAZGUWKWBZUYCUVSUYSVUAVUBU
      EZGUWKUDZWCZVUDUVSVUFCAUVSUVRUVPUYDVUFUHUVPUVQUVRWDUVPUVQUVRWECAGFWFWGWHV
      UDVUEWCZGUWKWBVUGVUCVUHGUWKVUAVUBWIWJVUEGUWKWKWLWMUVSVUCUYCGUWKUVSUYTUWKJ
      ZVUCUYCUXBUXADUWKUDZEUWKUDZUVSVUIVUCUFZUFZUYAUXADEUWKUWKWNVUMVUKUWFVUAVUB
      QRZSRZUYTBIZVUBQRZUWHSRZUEZDUWKUDZUYAVUIVUKVUTUPUVSVUCVUJVUTEUYTUWKEGWOZU
      XAVUSDUWKVVAUWQVUOUWTVURVVAUWPVUNUWFSVVAUWNVUAUWOVUBQUWMUYTCWPUWMUYTAWPZV
      NUTVVAUWSVUQUWHSVVAUWRVUPUWOVUBQUWMUYTBWPVVBVNWQWRVPWSWTUVSVULVUBXAJZVUPX
      AJZVUAXAJZKZVUCUFZVUTUYAUPUVSVUIVUCVVGUVSVUIUFZVVFVUCVVHVVCVVDVVEUVPUVQVU
      IVVCUVRAUYTFXBXCUVQUVPVUIVVDUVRBUYTFXBXDUVRUVPVUIVVEUVQCUYTFXBXEXFXGXHUVS
      VVGUFZVUTUWDVUQVUNXIRZUWHSRZUWEXJRZUEZDUWKUDZUYAVVIVVMVUSDUWKVVIUYJUFUWEU
      QJZUYLUYMKZVUBUQJZVUPUQJZVUAUQJZKZVUCVVMVUSUHUVSUYJVVPVVGUVSUYJUFVVOUYLUY
      MUVPUVQUYJVVOUVRAUWCFURXCUVQUVPUYJUYLUVRUYNXDUVRUVPUYJUYMUVQUYOXEXFXKVVGV
      VTUVSUYJVVFVVTVUCVVCVVQVVDVVRVVEVVSVUBXLZVUPXLVUAXLXMXNXOUVSVVFVUCUYJXPVV
      MVVLUWDUEZVVPVVTVUCKZVUSUWDVVLXQVWCVWBUWFVVKUEZUWFVURVUNXIRZUEZVUSVWCUYLV
      VOVVKUQJZVWBVWDUHVVOUYLUYMVVTVUCXRZVVOUYLUYMVVTVUCXSZVWCVVJUQJZUWHUQJZVWG
      VWCVUQUQJZVUNUQJZVUNTWAZVWJVWCVVRVVQVWLVVPVVQVVRVVSVUCXTVVPVVQVVRVVSVUCYA
      ZVUPVUBVBWGZVWCVVSVVQVWMVVPVVQVVRVVSVUCYBVWOVUAVUBVBWGZVVPVVTVWNVUCVVPVVT
      UFZVUNTVUAVUBVWRVVSVVQVUNTUEVUEUHZVVPVVQVVRVVSYCVVPVVQVVRVVSYDVUAVUBYEZWG
      YFYGZVUQVUNYHYIVWCUYMVVOVWKVVOUYLUYMVVTVUCYJVWIUWGUWEVBWGZVVJUWHYKWGUYLVV
      OVWGKVWDVWBUWDUWEVVKYLYMYIVWCVWEVVKUWFVWCVWLVWKVWMVWNVWEVVKUEVWPVXBVWQVXA
      VUQUWHVUNYNYOYPVWFVWEUWFUEZVWCVUSUWFVWEXQVWCVXCVUNUWFSRZVURUEZVUSVWCVURUQ
      JZUWFUQJZVWMVWNVXCVXEUHVWCVWLVWKVXFVWPVXBVUQUWHYKWGVWCUYLVVOVXGVWHVWIUWDU
      WEVBWGZVWQVXAVURUWFVUNYQYOVWCVXDVUOVURVWCVWMVXGVXDVUOUEVWQVXHVUNUWFYRWGYS
      ULYTUUAYTYIUUBUVSUVPUVRUFZVVJXAJZVVNUYAUPVVGUVPUVQUVRUUCVVGVUQXAJZVUNXAJZ
      VWNVXJVVGVVDVVCVXKVVCVVDVVEVUCUUDVVCVVDVVEVUCUUEZVUPVUBUUFWGVVGVVEVVCVXLV
      VCVVDVVEVUCUUGVXMVUAVUBUUFWGVVFVWNVUCVVFVUNTVUAVUBVVFVVSVVQVWSVVFVUAVVCVV
      DVVEWDUUHVVCVVDVVQVVEVWAUUIVWTWGYFUUJVUQVUNUUKYIVXIVXJUFUYAVVNVVLUWEQRZUW
      HSRZTUANZDUWKUDZUWGVVLQRZUWEVVLQRZSRZTUANZDUWKUDZUXJVVLUWGQRZSRZTUANZDUWK
      UDZOACDVVJFUULVVNUWLVXQUXHVYBUXNVYFVVNUWJVXPUHZDUWKUDUWLVXQUHVVMVYGDUWKVV
      MUWIVXOTUAVVMUWFVXNUWHSUWDVVLUWEQUUMWQVOUUNUWJVXPDUWKUUOVDVVNUXGVYAUHZDUW
      KUDUXHVYBUHVVMVYHDUWKVVMUXFVXTTUAVVMUXDVXRUXEVXSSUWDVVLUWGQUUPUWDVVLUWEQU
      UPVNVOUUNUXGVYADUWKUUOVDVVNUXMVYEUHZDUWKUDUXNVYFUHVVMVYIDUWKVVMUXLVYDTUAV
      VMUXKVYCUXJSUWDVVLUWGQUUMUTVOUUNUXMVYEDUWKUUOVDUOUUQVFUURUUSUUTUVAUVBUVCU
      VDUVEUVFUWLUXHUVGZUXBUFZUXOUVGZUXCUXIUVGZUXOUVGUYBUXPVYKVYMUXOUWLUXHUXBUV
      HUVIUYBVYJUXNUVGZUXBUFVYLUYAVYNUXBUWLUXHUXNUVJUVKVYJUXNUXBUVHUVLUXCUXIUXO
      UVJUVMUVNUL $.
      $( [24-Jun-2013] $)
  $}


  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesub.1 $e |- C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  (Contributed
       by Scott Fenton, 17-Jul-2013.) $)
    eleesub $p |- ( ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel wa c1 cfz co cv cmin cmpt cr wral fveere resubcl syl2an
      anandirs r19.21aiva wb cn eleenn mptelee syl adantr mpbird syl5eqel ) AEG
      HZIZBULIZJZCDKELMZDNZAHZUQBHZOMZPZULFUOVAULIZUTQIZDUPRZUOVCDUPUMUNUQUPIZV
      CUMVEJURQIUSQIVCUNVEJAUQESBUQESURUSTUAUBUCUMVBVDUDZUNUMEUEIVFAEUFURUSDOEU
      GUHUIUJUK $.
      $( [17-Jul-2013] $)
  $}

  ${
    $d N i $.  $d A i $.  $d B i $.
    eleesubd.1 $e |- ( ph ->
       C = ( i e. ( 1 ... N ) |-> ( ( A ` i ) - ( B ` i ) ) ) ) $.
    $( Membership of a subtraction mapping in a Euclidean space.  Deduction
       form of ~ eleesub .  (Contributed by Scott Fenton, 17-Jul-2013.) $)
    eleesubd $p |- ( ( ph /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
             C e. ( EE ` N ) ) $=
      ( cee cfv wcel w3a c1 cfz co cv cmin cmpt wceq 3ad2ant1 wa cr wral
      fveere resubcl syl2an anandirs r19.21aiva wb cn eleenn mptelee syl
      adantr mpbird 3adant1 eqeltrd ) ABFHIZJZCUQJZKDELFMNZEOZBIZVACIZPNZQZUQAU
      RDVERUSGSURUSVEUQJZAURUSTZVFVDUAJZEUTUBZVGVHEUTURUSVAUTJZVHURVJTVBUAJVCUA
      JVHUSVJTBVAFUCCVAFUCVBVCUDUEUFUGURVFVIUHZUSURFUIJVKBFUJVBVCEPFUKULUMUNUOU
      P $.
      $( [17-Jul-2013] $)
  $}

