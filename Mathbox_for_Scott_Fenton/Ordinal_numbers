$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Ordinal numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z A $.
    $( A class of transitive sets is partially ordered by ` _E ` . $)
    elpotr $p |- ( A. z e. A Tr z -> _E Po A ) $=
      ( vx vy wel wa wi wal wral cv wtr cep wpo alral alimi syl ralimi ralcom
      ralbii bitri sylib dftr2 wbr wn df-po epel anbi12i imbi12i elirrv mtbir
      biantrur bitr3i 2ralbii bitr4i 3imtr4i ) CDEZDAEZFZCAEZGZDHZCHZABIZUTABIZ
      DBIZCBIZAJZKZABIBLMZVCUTDBIZCBIZABIZVFVBVKABVBVJCHVKVAVJCUTDBNOVJCBNPQVLV
      JABIZCBIVFVJACBBRVMVECBUTADBBRSTUAVHVBABCDVGUBSVICJZVNLUCZUDZVNDJZLUCZVQV
      GLUCZFZVNVGLUCZGZFZABIZDBICBIVFCDABLUEVDWDCDBBUTWCABUTWBWCVTURWAUSVRUPVSU
      QCDUFDAUFUGCAUFUHVPWBVOCCECUICCUFUJUKULSUMUNUO $.
      $( [15-Oct-2010] $)
  $}

  ${
    $( Given ~ ax-reg , an ordinal is a transitive class totally ordered by
       epsilon. $)
    dford3 $p |- ( Ord A <-> ( Tr A /\ _E Or A ) ) $=
      ( word wtr cep wwe wa wor df-ord wfr df-we zfregfr mpbiran anbi2i bitri
      ) ABACZADEZFOADGZFAHPQOPADIQADJAKLMN $.
      $( [28-Jan-2011] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.
    $( Lemma for ~ dfon2 . $)
    dfon2lem1 $p |- Tr U. { x | ( ph /\ Tr x /\ ps ) } $=
      ( vy cv wtr w3a cab cuni truni wcel wsb hbs1 ax-17 hb3an visset weq
      sbequ12 treq 3anbi123d elabf simp2bi mprg ) DEZFZACEZFZBGZCHZIFDUIDUIJUDU
      IKACDLZUEBCDLZUHUJUEUKGCUDUJUEUKCACDMUECNBCDMODPCDQAUJUGUEBUKACDRUFUDSBCD
      RTUAUBUC $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d y ps $.
    $( Lemma for ~ dfon2 $)
    dfon2lem2 $p |- U. { x | ( x C_ A /\ ph /\ ps ) } C_ A $=
      ( cv wss w3a cab cpw cuni simp1 ss2abi df-pw sseqtr4i sspwuni mpbi ) CEDF
      ZABGZCHZDIZFSJDFSQCHTRQCQABKLCDMNSDOP $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d A x y z w t $.

    $( Lemma for ~ dfon2 .  All sets satisfying the new definition are
       transitive and untangled. $)
    dfon2lem3 $p |- ( A e. V -> ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) ->
                                        ( Tr A /\ A. z e. A -. z e. z ) ) ) $=
      ( vw vt vy wcel cv wpss wtr wa wi wal wel wn wral wss w3a cab cuni wceq
      untelirr wrex eluni2 visset weq sseq1 treq raleq 3anbi123d elab elequ1
      elequ2 bitrd notbid cbvralv biimpi 3ad2ant3 sylbi ra4 syl r19.23aiv mprg
      dfon2lem2 dfpss2 dfon2lem1 cvv ssexg mpan psseq1 anbi12d eleq1 imbi12d
      cla4gv imp sylan csuc csn snssg biimpd pm2.43i cun df-suc sseq1i unss
      bitr4i biimpri sylancr trsuc2 ax-mp untuni mprgbir ax-17 hbra1 hb3an
      hbab hbuni untsucf sucexg cbvabv eleq2i hbsuc raleqf elabg syl5bb
      biimprd com12 mp3an23 elssuni sucssel syl5 syld mpd syl6 mpan2i syl5bir
      mpani mt3i pm3.2i mpbii ex ) CDHZAIZCJZYDKZLZYDCHZMZANZCKZBBOZPZBCQZLZYCY
      JLZEIZCRZYQKZFFOZPZFYQQZSZETZUAZCUBZYOYPUUFUUEUUEHZYMUUGPBUUEBUUEUCBIZUUE
      HBAOZAUUDUDYMAUUHUUDUEUUIYMAUUDYDUUDHZYMBYDQZUUIYMMUUJYDCRZYFUUAFYDQZSZUU
      KUUCUUNEYDAUFEAUGYRUULYSYFUUBUUMYQYDCUHYQYDUIUUAFYQYDUJUKULUUMUULUUKYFUUM
      UUKUUAYMFBYDFBUGZYTYLUUOYTBFOYLFBFUMFBBUNUOUPUQURUSUTZYMBYDVAVBVCUTVDYPUU
      ECRZUUFPZUUGYSUUBECVEZUUQUURLUUECJZYPUUGUUECVFYPUUTUUEKZUUGYRUUBEVGZYPUUT
      UVALZUUECHZUUGYCUUEVHHZYJUVCUVDMZUUQYCUVEUUSUUECDVIVJUVEYJUVFYIUVFAUUEVHY
      DUUEUBZYGUVCYHUVDUVGYEUUTYFUVAYDUUECVKYDUUEUIVLYDUUECVMVNVOVPVQUVDUUEVRZC
      RZUUGUVDUUQUUEVSZCRZUVIUUSUVDUVKUVDUVDUVKUUECCVTWAWBUVIUUQUVKLZUVIUUEUVJW
      CZCRUVLUVHUVMCUUEWDWEUUEUVJCWFWGWHWIUVDUVIUVHUUDHZUUGUVIUVDUVNUVIUVHKZUUA
      FUVHQZUVDUVNMUVAUVOUVBUUEWJWKYMBUUEQZUVPUVQUUKAUUDBAUUDWLUUPWMZBFUUEFBUUD
      UUCFEBYRYSUUBFYRFWNYSFWNUUAFYQWOWPZWQWRWSWKUVDUVIUVOUVPSZUVNUVDUVHVHHZUVT
      UVNMUUECWTUWAUVNUVTUVNUVHUUHCRZUUHKZUUAFUUHQZSZBTZHUWAUVTUUDUWFUVHUUCUWEE
      BYQUUHUBYRUWBYSUWCUUBUWDYQUUHCUHYQUUHUIUUAFYQUUHUJUKXAXBUWEUVTBUVHVHUUHUV
      HUBUWBUVIUWCUVOUWDUVPUUHUVHCUHUUHUVHUIUUAFGUUHUVHGIUUHHFWNFGUUEFGUUDUUCFE
      GUVSWQWRXCXDUKXEXFXGVBXHXIXHUVNUVHUUERUVDUUGUVHUUDXJUUEUUECXKXLXMXNXOXPXQ
      XRXSUUFUVAUVQLYOUVAUVQUVBUVRXTUUFUVAYKUVQYNUUECUIYMBUUECUJVLYAVBYB $.
      $( [25-Feb-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    dfon2lem4.1 $e |- A e. _V $.
    dfon2lem4.2 $e |- B e. _V $.
    $( Lemma for ~ dfon2 .  If two sets satisfy the new definition, then one is
       a subset of the other. $)
    dfon2lem4 $p |- ( ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) /\
                         A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ->
                         ( A C_ B \/ B C_ A ) ) $=
      ( vz cv wpss wtr wa wcel wi wal cin wceq wo wss wn inss1 sseli wel wral
      cvv dfon2lem3 ax-mp simprd eleq1 eleq2 bitrd notbid rcla4cv syl adantr
      syl5 pm2.01d elin notbii sylib simpld trin syl2an inex1 psseq1 treq
      anbi12d imbi12d cla4v mpan2d adantl anim12d mtod ianor sspss mpbi inss2
      orel1 orc syl6 olc jaoa mp2ani df-ss sseqin2 orbi12i sylibr ) AHZCIZWGJZK
      ZWGCLZMZANZBHZDIZWNJZKZWNDLZMZBNZKZCDOZCPZXBDPZQZCDRZDCRZQXAXBCIZSZXBDIZS
      ZQZXEXAXHXJKZSXLXAXMXBCLZXBDLZKZXAXBXBLZSZXPSXAXQXQXNXAXRXBCXBCDTZUAWMXNX
      RMZWTWMGGUBZSZGCUCZXTWMCJZYCCUDLWMYDYCKMEAGCUDUEUFZUGYBXRGXBCGHZXBPZYAXQY
      GYAXBYFLXQYFXBYFUHYFXBXBUIUJUKULUMUNUOUPXQXPXBCDUQURUSXAXHXNXJXOXAXHXBJZX
      NWMYDDJZYHWTWMYDYCYEUTWTYIYBGDUCZDUDLWTYIYJKMFBGDUDUEUFUTCDVAVBZWMXHYHKZX
      NMZWTWLYMAXBCDEVCZWGXBPZWJYLWKXNYOWHXHWIYHWGXBCVDWGXBVEVFWGXBCUHVGVHUNVIX
      AXJYHXOYKWTXJYHKZXOMZWMWSYQBXBYNWNXBPZWQYPWRXOYRWOXJWPYHWNXBDVDWNXBVEVFWN
      XBDUHVGVHVJVIVKVLXHXJVMUSXLXHXCQZXJXDQZXEXBCRYSXSXBCVNVOXBDRYTCDVPXBDVNVO
      XIYSXEXKYTXIYSXCXEXHXCVQXCXDVRVSXKYTXDXEXJXDVQXDXCVTVSWAWBUMXFXCXGXDCDWCD
      CWDWEWF $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z $.  $d B x y z $.
    dfon2lem5.1 $e |- A e. _V $.
    dfon2lem5.2 $e |- B e. _V $.
    $( Lemma for ~ dfon2 .  Two sets satisfying the new definition also satisfy
       trichotomy with respect to ` e. ` $)
    dfon2lem5 $p |- ( ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) /\
                         A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ->
                       ( A e. B \/ A = B \/ B e. A ) ) $=
      ( vz cv wpss wtr wa wcel wi wal wceq wn wo w3o wss dfon2lem4 dfpss2
      eqcom notbii anbi2i bitri orbi12i andir bitr4i orcom wel wral cvv
      dfon2lem3 ax-mp simpld psseq1 treq anbi12d eleq1 imbi12d cla4v exp3a
      com23 imp sylan2 mpan9 orim12d syl5bi syl5bir mpand 3orrot 3orass df-or
      3bitri sylibr ) AHZCIZVPJZKZVPCLZMZANZBHZDIZWCJZKZWCDLZMZBNZKZCDOZPZDCLZC
      DLZQZMZWNWKWMRZWJCDSZDCSZQZWLWOABCDEFTWTWLKZCDIZDCIZQZWJWOXDWRWLKZWSWLKZQ
      XAXBXEXCXFCDUAXCWSDCOZPZKXFDCUAXHWLWSXGWKDCUBUCUDUEUFWRWSWLUGUHXDXCXBQWJW
      OXBXCUIWJXCWMXBWNWIWBDJZXCWMMZWIXIGGUJPZGDUKZDULLWIXIXLKMFBGDULUMUNUOWBXI
      XJWBXCXIWMWBXCXIWMWAXCXIKZWMMADFVPDOZVSXMVTWMXNVQXCVRXIVPDCUPVPDUQURVPDCU
      SUTVAVBVCVDVEWBCJZWIXBWNMWBXOXKGCUKZCULLWBXOXPKMEAGCULUMUNUOWIXBXOWNWIXBX
      OWNWHXBXOKZWNMBCEWCCOZWFXQWGWNXRWDXBWEXOWCCDUPWCCUQURWCCDUSUTVAVBVCVFVGVH
      VIVJWQWKWMWNRWKWOQWPWNWKWMVKWKWMWNVLWKWOVMVNVO $.
      $( [25-Feb-2011] $)
  $}

  ${
    $d S x y z w s t $.
    $( Lemma for ~ dfon2 .  A transitive class of sets satisfying the new
       definition satisfies the new definition. $)
    dfon2lem6 $p |- ( ( Tr S /\
                         A. x e. S A. z ( ( z C. x /\ Tr z ) -> z e. x ) ) ->
                       A. y ( ( y C. S /\ Tr y ) -> y e. S ) ) $=
      ( vs vw vt wtr cv wpss wa wel wi wal wral wcel weq wo cdif wss wn pssss
      ssralv syl impcom adantrr ad2ant2lr psseq2 anbi1d elequ2 imbi12d albidv
      rcla4cv imp eldifi rcla4v psseq1 treq anbi12d elequ1 cbvalv syl6ib
      ad2ant2l adantr w3o visset dfon2lem5 3orrot 3orass bitri eleq1a elndif
      nsyli adantll orel1 trss eldifn ssel con3d syl5com syl9 adantl imp31
      syl9r mpd syl5bi syl5 mp2and ex ssrdv dfpss2 a4v exp3a com23 syl6 com3l
      adantld imp32 syl5bir mpand orrd anassrs r19.21aiva wrex c0 wne df-pss
      pssdifn0 sylbi r19.2z ad2antrl eleq1 syl5ibr a1i trel syl7 ad2antrr jaod
      r19.23adv syld 19.21aiv ) DHZCIZAIZJZYCHZKZCALZMZCNZADOZKZBIZDJZYMHZKZYMD
      PZMBYLYPYQYLYPKZBEQZBELZRZEDYMSZOZYQYRUUAEUUBYLYPEIZUUBPZUUAYLYPUUEKZKZYS
      YTUUGYMUUDTZYSUAZYTUUGFYMUUDUUGFBLZFELZUUGUUJKZYCFIZJZYFKZCFLZMZCNZGIZUUD
      JZUUSHZKZGELZMZGNZUUKUUGUUJUURUUGYJAYMOZUUJUURMYKYPUVFYBUUEYKYNUVFYOYNYKU
      VFYNYMDTZYKUVFMYMDUBYJAYMDUCUDUEUFUGYJUURAUUMYMAFQZYIUUQCUVHYGUUOYHUUPUVH
      YEUUNYFYDUUMYCUHUIAFCUJUKULUMUDUNUUGUVEUUJYKUUEUVEYBYPUUEYKUVEUUEYKYCUUDJ
      ZYFKZCELZMZCNZUVEUUEUUDDPZYKUVMMUUDDYMUOZYJUVMAUUDDAEQZYIUVLCUVPYGUVJYHUV
      KUVPYEUVIYFYDUUDYCUHUIAECUJUKULUPUDZUVLUVDCGCGQZUVJUVBUVKUVCUVRUVIUUTYFUV
      AYCUUSUUDUQYCUUSURUSCGEUTUKVAVBUEVCVDUURUVEKUUKFEQZEFLZVEZUULUUKCGUUMUUDF
      VFEVFVGUWAUVSUVTUUKRZRZUULUUKUWAUVSUVTUUKVEUWCUUKUVSUVTVHUVSUVTUUKVIVJUUL
      UVSUAZUWCUUKMUUFUUJUWDYLUUEUUJUWDYPUUEUUJUWDUUEUVSUUMUUBPUUJUUDUUBUUMVKUU
      MYMDVLVMUNVNVNUWDUWCUWBUULUUKUVSUWBVOUULUVTUAZUWBUUKMUUFUUJUWEYLYPUUEUUJU
      WEYOUUEUUJUWEMMYNYOUUJUUMYMTZUUEUWEYMUUMVPUUEEBLZUAUWFUWEUUDDYMVQUWFUVTUW
      GUUMYMUUDVRVSVTWAWBWCVNUVTUUKVOUDWDWEWFWGWHWIWJUUHUUIKYMUUDJZUUGYTYMUUDWK
      YLYPUUEUWHYTMZYKYPUUEUWIMZMYBYKYOUWJYNUUEYKYOUWIUUEYKUVMYOUWIMUVQUVMUWHYO
      YTUVMUWHYOYTUVLUWHYOKZYTMCBCBQZUVJUWKUVKYTUWLUVIUWHYFYOYCYMUUDUQYCYMURUSC
      BEUTUKWLWMWNWOWPWQWBWRWSWTXAXBXCYRUUCUUAEUUBXDZYQYNUUCUWMMZYLYOYNUUBXEXFZ
      UWNYNUVGYMDXFKUWOYMDXGYMDXHXIUWOUUCUWMUUAEUUBXJWIUDXKYRUUAYQEUUBYRUUAUUEY
      QYRYSUUEYQMZYTYSUWPMYRUUEYQYSUVNUVOYMUUDDXLXMXNYBYTUWPMYKYPUUEUVNYBYTYQUV
      OYBYTUVNYQDYMUUDXOWMXPXQXRWNXSXTWEWIYA $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z w s t u $.  $d B x y z w t $.
    dfon2lem7.1 $e |- A e. _V $.
    $( Lemma for ~ dfon2 .  All elements of a new ordinal are new ordinals. $)
    dfon2lem7 $p |- ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) ->
                       ( B e. A -> A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ) $=
      ( vw vt vu vz vs cv wpss wtr wa wcel wi wal wss wel wral w3a cab cuni
      wceq csuc csn wn weq elequ1 elequ2 bitrd notbid cbvralv biimpi ralimi
      untuni sylibr visset sseq1 treq raleq 3anbi123d elab cvv dfon2lem3 ax-mp
      simprd untelirr syl 3ad2ant3 sylbi mprg psseq2 anbi1d imbi12d albidv
      3anbi3i abbii unieqi eleq2i notbii sylib dfon2lem2 ssexi snss mtbi
      intnan cun df-suc sseq1i unss bitr4i mtbir dfon2lem1 trsuc2 wo elsuc
      wrex eluni2 hba1 rcla4cv psseq1 anbi12d cbvalv syl6ib r19.23ai r19.23aiv
      rgen dfon2lem6 mp2an eleq2 mpbiri jaoi sucex elssuni sylbir mp3an23
      ralbii syl6bb cbvabv syl6sseq a1i syl5bir mpani syl5bi mtoi eleq1 cla4v
      mpan2i mtod dfpss2 biimpri mpan nsyl2 ra4 mpbii ) AKZCLZYQMZNZYQCOZPZAQZF
      KZCRZUUDMZBKZGKZLZUUGMZNZBGSZPZBQZGUUDTZUAZFUBZUCZCUDZDCOUUGDLZUUJNZUUGDO
      ZPZBQZPZUUCUURCLZUUSUUCUVFUURCOZUUCUVGUURUEZUUEUUFUUGHKZLZUUJNZBHSZPZBQZH
      UUDTZUAZFUBZUCZRZUVSUURUVRRZUURUFZUVRRZNZUWBUVTUURUVROZUWBIISZUGZIUURTZUW
      DUGZGGSZUGZGYQTZUWGAUUQUWKAUUQTUWFIYQTZAUUQTUWGUWKUWLAUUQUWKUWLUWJUWFGIYQ
      GIUHZUWIUWEUWMUWIIGSUWEGIGUIGIIUJUKULUMUNUOIAUUQUPUQYQUUQOZYQCRZYSUUNGYQT
      ZUAZUWKUUPUWQFYQAURZFAUHZUUEUWOUUFYSUUOUWPUUDYQCUSZUUDYQUTZUUNGUUDYQVAZVB
      VCZUWPUWOUWKYSUUNUWJGYQUUNHHSUGHUUHTZUWJUUNUUHMZUXDUUHVDOUUNUXEUXDNPGURBH
      UUHVDVEVFVGHUUHVHVIUOVJVKVLUWGUURUUROZUGUWHIUURVHUXFUWDUURUVRUURUUQUVQUUP
      UVPFUUOUVOUUEUUFUUNUVNGHUUDGHUHZUUMUVMBUXGUUKUVKUULUVLUXGUUIUVJUUJUUHUVIU
      UGVMVNGHBUJVOVPZUMVQVRVSVTWAWBVFUURUVRUURCEUUFUUOFCWCZWDZWEWFWGUVSUURUWAW
      HZUVRRUWCUVHUXKUVRUURWIZWJUURUWAUVRWKWLWMUVGUWACRZUUCUVSUURCUXJWEUUCUURCR
      ZUXMUVSUXIUXNUXMNZUVHCRZUUCUVSUXPUXKCRUXOUVHUXKCUXLWJUURUWACWKWLUXPUVSPUU
      CUXPUVHJKZCRZUXQMZYQUVILZYSNZAHSZPZAQZHUXQTZUAZJUBZUCZUVRUXPUVHMZUYDHUVHT
      ZUVHUYHRZUURMZUYIUUEUUOFWNZUURWOVFUYDHUVHUVIUVHOUVIUUROZUVIUURUDZWPUYDUVI
      UURHURWQUYNUYDUYOUYNHASZAUUQWRZUYDAUVIUUQWSZUYPUYDAUUQUYCAWTUWNUWQUYPUYDP
      ZUXCUWPUWOUYSYSUWPUYPUVNUYDUUNUVNGUVIYQUXHXAZUVMUYCBABAUHZUVKUYAUVLUYBVUA
      UVJUXTUUJYSUUGYQUVIXBUUGYQUTXCBAHUIVOXDXEVJVKXFVKUYOUYDYQUURLZYSNZYQUUROZ
      PZAQZUYLIKZUVILZVUGMZNZIHSZPZIQZHUURTVUFUYMVUMHUURUYNUYQVUMUYRUYPVUMAUUQU
      WNUWQUYPVUMPZUXCUWPUWOVUNYSUWPUYPUVNVUMUYTUVMVULBIBIUHZUVKVUJUVLVUKVUOUVJ
      VUHUUJVUIUUGVUGUVIXBUUGVUGUTXCBIHUIVOXDXEVJVKXGVKXHHAIUURXIXJUYOUYCVUEAUY
      OUYAVUCUYBVUDUYOUXTVUBYSUVIUURYQVMVNUVIUURYQXKVOVPXLXMVKXHUXPUYIUYJUAZUVH
      UYGOUYKUYFVUPJUVHUURUXJXNUXQUVHUDUXRUXPUXSUYIUYEUYJUXQUVHCUSUXQUVHUTUYDHU
      XQUVHVAVBVCUVHUYGXOXPXQUYGUVQUYFUVPJFJFUHZUXRUUEUXSUUFUYEUVOUXQUUDCUSUXQU
      UDUTVUQUYEUYDHUUDTUVOUYDHUXQUUDVAUYDUVNHUUDUYCUVMABABUHZUYAUVKUYBUVLVURUX
      TUVJYSUUJYQUUGUVIXBYQUUGUTXCABHUIVOXDXRXSVBXTVSYAYBYCYDYEYFUUCUVFUYLUVGUY
      MUUBUVFUYLNZUVGPAUURUXJYQUURUDZYTVUSUUAUVGVUTYRUVFYSUYLYQUURCXBYQUURUTXCY
      QUURCYGVOYHYIYJUXNUUSUGZUVFUXIUVFUXNVVANUURCYKYLYMYNUUSUUGVUGLZUUJNZBISZP
      ZBQZICTZUVEUUSVVFIUURTVVGVVFIUURVUGUUROIASZAUUQWRVVFAVUGUUQWSVVHVVFAUUQUW
      NUWOYSVVFIYQTZUAZVVHVVFPZUUPVVJFYQUWRUWSUUEUWOUUFYSUUOVVIUWTUXAUWSUUOUWPV
      VIUXBUUNVVFGIYQUWMUUMVVEBUWMUUKVVCUULVVDUWMUUIVVBUUJUUHVUGUUGVMVNGIBUJVOV
      PUMXSVBVCVVIUWOVVKYSVVFIYQYOVJVKXGVKXHVVFIUURCVAYPVVFUVDIDCVUGDUDZVVEUVCB
      VVLVVCUVAVVDUVBVVLVVBUUTUUJVUGDUUGVMVNVUGDUUGXKVOVPXAVIVI $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z w s t $.
    $( Lemma for ~ dfon2 .  The intersection of a non-empty class ` A ` of new
       ordinals is itself a new ordinal and is contained within ` A ` $)
    dfon2lem8 $p |- ( ( A =/= (/) /\
                         A. x e. A A. y ( ( y C. x /\ Tr y ) -> y e. x ) ) ->
                       ( A. z ( ( z C. |^| A /\ Tr z ) -> z e. |^| A ) /\
                         |^| A e. A ) ) $=
      ( vt vw c0 wne cv wpss wtr wa wel wi wal wral cint wcel wn cvv visset
      dfon2lem3 ax-mp simpld ralimi trint syl adantl cuni dfon2lem7 19.21aiv
      df-ral 19.21v albii bitr4i impexp 2albii wrex eluni2 biimpi imim1i alimi
      alcom wex 19.23v df-rex imbi1i bitri 3imtr4i sylbir sylbi wss intssuni
      ssralv adantr mpd dfon2lem6 intex imp simprd untelirr adantlr wceq
      risset notbii ralnex eqcom weq psseq2 anbi1d elequ2 imbi12d albidv
      rcla4cv intss1 dfpss2 psseq1 treq anbi12d eleq1 cla4gv exp3a syl5bir
      exp4b com45 com23 syl5 mpdd mpid syl7bi r19.21aiv ralim syl5bi wb elintg
      ad2antrr sylibrd mt3d ex ancld mp2and ) DGHZBIZAIZJZYCKZLZBAMZNZBOZADPZLZ
      DQZKZEIZFIZJYOKLEFMNEOZFYMPZCIZYMJYSKLYSYMRNCOZYMDRZLZYKYNYBYKYDKZADPYNYJ
      UUCADYJUUCCCMSCYDPZYDTRYJUUCUUDLNAUAZBCYDTUBUCUDUEADUFUGUHYLYQFDUIZPZYRYK
      UUGYBYKFAMZYQNZFOZADPZUUGYJUUJADYJUUIFBEYDYPUUEUJUKUEUUKYDDRZUUINZFOZAOZU
      UGUUKUULUUJNZAOUUOUUJADULUUNUUPAUULUUIFUMUNUOUUOUULUUHLZYQNZFOAOZUUGUURUU
      MAFUULUUHYQUPUQUUHADURZYQNZFOZYPUUFRZYQNZFOUUSUUGUVAUVDFUVCUUTYQUVCUUTAYP
      DUSUTVAVBUUSUURAOZFOUVBUURAFVCUVEUVAFUVEUUQAVDZYQNUVAUUQYQAVEUUTUVFYQUUHA
      DVFVGUOUNVHYQFUUFULVIVJVKUGUHYBUUGYRNZYKYBYMUUFVLUVGDVMYQFYMUUFVNUGVOVPYN
      YRLYTYLUUBFCEYMVQYLYTUUAYLYTUUAYLYTLZUUAYMYMRZYBYTUVISZYKYBYTLZEEMSEYMPZU
      VJUVKYNUVLYBYTYNUVLLZYBYMTRZYTUVMNDVRZCEYMTUBVKVSZVTEYMWAUGWBUVHUUASZYMYO
      RZEDPZUVIUVQYOYMWCZSZEDPZUVHUVSUVQUVTEDURZSUWBUUAUWCEYMDWDWEUVTEDWFUOUVHU
      WAUVRNZEDPUWBUVSNUVHUWDEDUWAYMYOWCZSZUVHYODRZUVRUVTUWEYOYMWGWEUVHUWGYNUWF
      UVRNZYBYTYNYKUVKYNUVLUVPUDWBYLUWGYNUWHNZNYTYLUWGYCYOJZYFLZBEMZNZBOZUWIYKU
      WGUWNNYBYJUWNAYODAEWHZYIUWMBUWOYGUWKYHUWLUWOYEUWJYFYDYOYCWIWJAEBWKWLWMWNU
      HYBUWGUWNUWINZNYKUWGYMYOVLZYBUWPYODWOYBUWNUWQUWIYBUWNUWQUWFYNUVRYBUWNUWQU
      WFYNUVRNZUWQUWFLYMYOJZYBUWNLZUWRYMYOWPUWTUWSYNUVRYBUWNUWSYNLZUVRNZYBUVNUW
      NUXBNUVOUWMUXBBYMTYCYMWCZUWKUXAUWLUVRUXCUWJUWSYFYNYCYMYOWQYCYMWRWSYCYMYOW
      TWLXAVKVSXBXCXDXEXFXGVOXHVOXIXJXKUWAUVREDXLUGXMYBUVIUVSXNZYKYTYBUVNUXDUVO
      EYMDTXOVKXPXQXRXSXTXGYA $.
      $( [26-Feb-2011] $)
  $}

  ${
    $d A x y z w s t u v $.
    $( Lemma for ~ dfon2 .  A class of new ordinals is well-founded by
       ` _E ` . $)
    dfon2lem9 $p |- ( A. x e. A A. y ( ( y C. x /\ Tr y ) -> y e. x )
                         -> _E Fr A ) $=
      ( vz vt vw vu cv wpss wtr wa wel wi wal wral wss c0 wne wn wrex cep wfr
      ssralv cint wcel dfon2lem8 simprd intss1 simpld cvv intex wceq dfon2lem3
      imp untelirr syl eleq1 notbid syl5ibcom a1dd trss eqss biimpri expcom
      syl6 com23 con3 pm2.61d sylanb syldan syl5 r19.21aiv eleq2 ralbidv
      rcla4ev syl11anc syl6com imp3a 19.21aiv wbr df-fr epel notbii ralbii
      rexbii imbi2i albii bitri sylibr ) BHZAHIWJJKBALMBNZACOZDHZCPZWMQRZKZEFLZ
      SZEWMOZFWMTZMZDNZCUAUBZWLXADWLWNWOWTWNWLWKAWMOZWOWTMWKAWMCUCWOXDWTWOXDKZW
      MUDZWMUEZEHZXFUEZSZEWMOZWTXEGHZXFIXLJKXLXFUEMGNZXGABGWMUFZUGXEXJEWMEDLXFX
      HPZXEXJXHWMUHWOXDXMXOXJMZXEXMXGXNUIWOXFUJUEZXMXPWMUKXQXMKZXFXHULZXPXRXSXJ
      XOXRXFXFUEZSZXSXJXRAALSAXFOZYAXRXFJZYBXQXMYCYBKGAXFUJUMUNZUGAXFUOUPXSXTXI
      XFXHXFUQURUSUTXRXOXSSZXJXRXOXIXSMYEXJMXRXIXOXSXRXIXHXFPZXOXSMXRYCXIYFMXRY
      CYBYDUIXFXHVAUPXOYFXSXSXOYFKXFXHVBVCVDVEVFXIXSVGVEVFVHVIVJVKVLWSXKFXFWMFH
      ZXFULZWRXJEWMYHWQXIYGXFXHVMURVNVOVPVDVQVRVSXCWPXHYGUAVTZSZEWMOZFWMTZMZDNX
      BDFECUAWAYMXADYLWTWPYKWSFWMYJWREWMYIWQEFWBWCWDWEWFWGWHWI $.
      $( [3-Mar-2011] $)
  $}

  ${
    $d x y z w t u v $.
    $( ` On ` consists of all sets that contain all its transitive proper
       subsets.  This definition comes from J. R. Isbell, "A Definition of
       Ordinal Numbers," American Mathematical Monthly, vol 67 (1960), pp.
       51-52. $)
    dfon2 $p |- On = { x | A. y ( ( y C. x /\ Tr y ) -> y e. x ) } $=
      ( vz vw vu vt vv con0 cv word cab wpss wtr wa wel wi wal df-on wss wne
      tz7.7 df-pss syl6bbr exbiri com23 imp3a 19.21aiv cep wwe wn wral cvv
      wcel visset dfon2lem3 ax-mp simpld wfr weq w3o dfon2lem7 r19.21aiv
      dfon2lem9 psseq2 anbi1d elequ2 imbi12d albidv psseq1 treq anbi12d elequ1
      cbvalv syl6bb rcla4cv anim12d dfon2lem5 syl6 r19.21aivv jca syl wbr
      dfwe2 epel biid 3orbi123i 2ralbii anbi2i bitri sylibr df-ord sylanbrc
      impbii abbii eqtri ) HAIZJZAKBIZWPLZWRMZNBAOZPZBQZAKARWQXCAWQXCWQXBBWQWSW
      TXAWQWTWSXAWQWTXAWSWQWTNXAWRWPSWRWPTNWSWPWRUAWRWPUBUCUDUEUFUGXCWPMZWPUHUI
      ZWQXCXDCCOUJCWPUKZWPULUMXCXDXFNPAUNZBCWPULUOUPUQXCWPUHURZCDOZCDUSZDCOZUTZ
      DWPUKCWPUKZNZXEXCEIZFIZLZXOMZNZEFOZPZEQZFWPUKZXNXCYBFWPBEWPXPXGVAVBYCXHXM
      FEWPVCYCXLCDWPWPYCCAOZDAOZNGIZCIZLZYFMZNZGCOZPZGQZWRDIZLZWTNZBDOZPZBQZNXL
      YCYDYMYEYSYBYMFYGWPFCUSZYBXOYGLZXRNZECOZPZEQYMYTYAUUDEYTXSUUBXTUUCYTXQUUA
      XRXPYGXOVDVEFCEVFVGVHUUDYLEGEGUSZUUBYJUUCYKUUEUUAYHXRYIXOYFYGVIXOYFVJVKEG
      CVLVGVMVNVOYBYSFYNWPFDUSZYBXOYNLZXRNZEDOZPZEQYSUUFYAUUJEUUFXSUUHXTUUIUUFX
      QUUGXRXPYNXOVDVEFDEVFVGVHUUJYREBEBUSZUUHYPUUIYQUUKUUGYOXRWTXOWRYNVIXOWRVJ
      VKEBDVLVGVMVNVOVPGBYGYNCUNDUNVQVRVSVTWAXEXHYGYNUHWBZXJYNYGUHWBZUTZDWPUKCW
      PUKZNXNCDWPUHWCUUOXMXHUUNXLCDWPWPUULXIXJXJUUMXKCDWDXJWEDCWDWFWGWHWIWJWPWK
      WLWMWNWO $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d x y $.
    $( The domain of the epsilon relation is the universe. $)
    domep $p |- dom _E = _V $=
      ( vx vy cep cdm cv wbr wex cab cvv df-dm wceq df-v equid wcel el epel
      exbii mpbir 2th abbii eqtri eqtr4i ) CDAEZBEZCFZBGZAHZIABCJIUCUCKZAHUGALU
      HUFAUHUFAMUFUCUDNZBGABOUEUIBABPQRSTUAUB $.
      $( [27-Oct-2010] $)
  $}

  $( Ordinal two is not zero. $)
  2on0 $p |- 2o =/= (/) $=
    ( c2o c1o csuc c0 df-2o nsuceq0 eqnetri ) ABCDEBFG $.
    $( [10-Jul-2011] $) $( [17-Jun-2011] $)

  ${
    $d A x $.  $d B x $.
    $( Given an element ` A ` of the union of an ordinal ` B ` , ` suc A ` is
       an element of ` B ` itself. $)
    ordsucuniel $p |- ( Ord B -> ( A e. U. B <-> suc A e. B ) ) $=
      ( vx word cuni wcel csuc c0 wne wa cv wex eluni ne0i adantl 19.23aiv
      sylbi anim2i wceq con0 wrex wlim wo wb wn wi df-ne w3o ordzsl 3orel1
      syl5bi impcom eloni ordsucelsuc syl ordunisuc eleq2 bibi1d biimprcd sylc
      unieq eleq2d bibi12d syl5ibrcom r19.23aiv limsuc limuni biimpcd jaoi
      pm5.21nd ) BDZABEZFZAGZBFZVKBHIZJZVMVPVKVMACKZFZVRBFZJZCLVPCABMWAVPCVTVPV
      SBVRNOPQRVOVPVKBVNNRVQBVRGZSZCTUAZBUBZUCZVMVOUDZVPVKWFVPBHSZUEZVKWFUFBHUG
      VKWHWDWEUHWIWFCBUIWHWDWEUJUKQULWDWGWEWCWGCTVRTFZWGWCAWBEZFZVNWBFZUDZWJVSW
      MUDZWKVRSZWNWJVRDZWOVRUMZAVRUNUOWJWQWPWRVRUPUOWPWNWOWPWLVSWMWKVRAUQURUSUT
      WCVMWLVOWMWCVLWKABWBVAVBBWBVNUQVCVDVEWEABFZVOUDZBVLSZWGBAVFBVGXAWTWGXAWSV
      MVOBVLAUQURVHUTVIUOVJ $.
      $( [11-Apr-2012] $) $( [28-Mar-2012] $)
  $}

  ${
    nncli.1 $e |- A e. om $.
    nncli.2 $e |- B e. om $.
    $( ` om ` is closed under addition.  Inference form of ~ nnacl . $)
    nnacli $p |- ( A +o B ) e. om $=
      ( com wcel coa co nnacl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [12-May-2012] $) $( [20-Apr-2012] $)

    $( ` om ` is closed under multiplication.  Inference form of ~ nnmcl . $)
    nnmcli $p |- ( A .o B ) e. om $=
      ( com wcel comu co nnmcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [12-May-2012] $) $( [20-Apr-2012] $)

  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    $( Adding to both sides of an inequality in ` om ` $)
    omssadd $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
             ( A C_ B -> ( A +o C ) C_ ( B +o C ) ) ) $=
      ( vx vy com wcel wss coa co wi wa cv c0 csuc wceq opreq2 sseq12d imbi2d
      weq con0 nnon oa0 adantr adantl biimprd syl2an word wb nnacl ancoms
      adantrr syl eloni adantrl ordsucsssuc syl11anc biimpa nnasuc mpbird ex
      imim2d a2d finds com12 3impia ) AFGZBFGZCFGZABHZACIJZBCIJZHZKZVIVGVHLZVNV
      OVJADMZIJZBVPIJZHZKZKVOVJANIJZBNIJZHZKZKVOVJAEMZIJZBWEIJZHZKZKVOVJAWEOZIJ
      ZBWJIJZHZKZKVOVNKDECVPNPZVTWDVOWOVSWCVJWOVQWAVRWBVPNAIQVPNBIQRSSDETZVTWIV
      OWPVSWHVJWPVQWFVRWGVPWEAIQVPWEBIQRSSVPWJPZVTWNVOWQVSWMVJWQVQWKVRWLVPWJAIQ
      VPWJBIQRSSVPCPZVTVNVOWRVSVMVJWRVQVKVRVLVPCAIQVPCBIQRSSVGAUAGZBUAGZWDVHAUB
      BUBWSWTLZWCVJXAWAAWBBWSWAAPWTAUCUDWTWBBPWSBUCUERUFUGWEFGZVOWIWNXBVOWIWNKX
      BVOLZWHWMVJXCWHWMXCWHLWMWFOZWGOZHZXCWHXFXCWFUHZWGUHZWHXFUIXCWFUAGZXGXCWFF
      GZXIXBVGXJVHVGXBXJAWEUJUKULWFUBUMWFUNUMXCWGUAGZXHXCWGFGZXKXBVHXLVGVHXBXLB
      WEUJUKUOWGUBUMWGUNUMWFWGUPUQURXCWMXFUIWHXCWKXDWLXEXBVGWKXDPZVHVGXBXMAWEUS
      UKULXBVHWLXEPZVGVHXBXNBWEUSUKUORUDUTVAVBVAVCVDVEVF $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( Multiply an ordinal by ` 2o ` $)
  onm2 $p |- ( A e. On -> ( A .o 2o ) = ( A +o A ) ) $=
    ( con0 wcel c2o comu co c1o csuc coa df-2o opreq2i wceq 1on omsuc mpan2
    om1 opreq1d eqtrd syl5eq ) ABCZADEFAGHZEFZAAIFZDUAAEJKTUBAGEFZAIFZUCTGBCUBU
    ELMAGNOTUDAAIAPQRS $.
    $( [12-May-2012] $) $( [18-Apr-2012] $)

  $( Multiply an element of ` om ` by ` 2o ` $)
  omm2 $p |- ( A e. om -> ( 2o .o A ) = ( A +o A ) ) $=
    ( com wcel c2o comu co coa wceq 2onn nnmcom mpan con0 nnon onm2 syl eqtrd
    ) ABCZDAEFZADEFZAAGFZDBCQRSHIDAJKQALCSTHAMANOP $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    omopthlem1.1 $e |- A e. om $.
    omopthlem1.2 $e |- C e. om $.
    $( Lemma for ~ omopthi . $)
    omopthlem1 $p |- ( A e. C ->
                ( ( A .o A ) +o ( A .o 2o ) ) e. ( C .o C ) ) $=
      ( csuc wss comu co wcel c2o coa con0 wi com peano2 ax-mp nnoni omwordi
      mp3an omwordri sstrd onsucssi nnmcli 2onn nnacli wceq nnasuc mp2an
      nnmsuc nnaass nnmcom eqtri opreq1i onm2 opreq2i 3eqtr4ri suceq sseq1i
      bitri 3imtr4i ) AEZBFZVAVAGHZBBGHZFZABIAAGHZAJGHZKHZVDIZVBVCVABGHZVDVALIZ
      BLIZVKVBVCVJFMVAANIZVANIZCAOPZQZBDQZVPVABVARSVKVLVLVBVJVDFMVPVQVQVABBTSUA
      ABACQZVQUBVIVHEZVDFVEVHVDVHVFVGAACCUCZAJCUDUCUEQVDBBDDUCQUBVSVCVDVAAGHZVA
      KHZWAAKHZEZVCVSWANIVMWBWDUFVAAVOCUCCWAAUGUHVNVMVCWBUFVOCVAAUIUHVHWCUFVSWD
      UFVFAKHZAKHZVFAAKHZKHZWCVHVFNIVMVMWFWHUFVTCCVFAAUJSWAWEAKWAAVAGHZWEVNVMWA
      WIUFVOCVAAUKUHVMVMWIWEUFCCAAUIUHULUMVGWGVFKALIVGWGUFVRAUNPUOUPVHWCUQPUPUR
      USUT $.
      $( [12-May-2012] $) $( [18-Apr-2012] $)


  $}

  ${
    omopthlem2.1 $e |- A e. om $.
    omopthlem2.2 $e |- B e. om $.
    omopthlem2.3 $e |- C e. om $.
    omopthlem2.4 $e |- D e. om $.
    $( Lemma for ~ omopthi . $)
    omopthlem2 $p |- ( ( A +o B ) e. C ->
     -. ( ( C .o C ) +o D ) = ( ( ( A +o B ) .o ( A +o B ) ) +o B ) ) $=
      ( coa co wcel comu wceq wn c2o wss com nnacli nnaword1 mp2an nnacom
      sseqtri con0 nnoni onm2 nnaass mp3an syl6eqr ax-mp sseqtr4i wi 2onn
      nnmcli omssadd 3sstr4i omopthlem1 wa ontr2 sylancr sseli onirri eleq1
      mtbii con2i 3syl ) ABIJZCKZVFVFLJZBIJZCCLJZKZVIVJDIJZKZVLVIMZNVGVIVHVFOLJ
      ZIJZPZVPVJKZVKBVHIJZVOVHIJZVIVPBVOPZVSVTPZBVFAIJZBIJZVOBBWCIJZWDBQKZWCQKZ
      BWEPFVFAABEFRZERZBWCSTWFWGWEWDMFWIBWCUATUBVFUCKZVOWDMVFWHUDWJVOVFVFIJZWDV
      FUEVFQKAQKWFWDWKMWHEFVFABUFUGUHUIUJWFVOQKZVHQKZWAWBUKFVFOWHULUMZVFVFWHWHU
      MZBVOVHUNUGUIWMWFVIVSMWOFVHBUATWMWLVPVTMWOWNVHVOUATUOVFCWHGUPVIUCKVJUCKVQ
      VRUQVKUKVIVHBWOFRUDVJCCGGUMZUDVIVPVJURTUSVJVLVIVJQKDQKVJVLPWPHVJDSTUTVNVM
      VNVLVLKVMVLVLVJDWPHRUDVAVLVIVLVBVCVDVE $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    omopth.1 $e |- A e. om $.
    omopth.2 $e |- B e. om $.
    omopth.3 $e |- C e. om $.
    omopth.4 $e |- D e. om $.
    $( An ordered pair theorem for ` om ` .  Theorem 17.3 of [Quine] p. 124.
       This proof is adapted from ~ nn0opthi . $)
    omopthi $p |- ( ( ( ( A +o B ) .o ( A +o B ) ) +o B ) =
                     ( ( ( C +o D ) .o ( C +o D ) ) +o D ) <->
                     ( A = C /\ B = D ) ) $=
      ( coa co comu wceq wa wn wcel wo word wb nnacli nnoni onordi ordtri3
      mp2an con2bii omopthlem2 eqcom notbii sylib jaoi sylbir con4i id
      opreq12d opreq1d eqtr4d com nnmcli nnacan mp3an opreq2d nnacom 3eqtr4g
      con0 oacan jca opreq12 simpr impbii ) ABIJZVIKJZBIJZCDIJZVLKJZDIJZLZACLZB
      DLZMZVOVPVQVOBAIJZBCIJZLZVPVOVICBIJZVSVTVOVIVLWBVIVLLZVOWCNVIVLOZVLVIOZPZ
      VONZWCWFVIQVLQWCWFNRVIVIABEFSZTUAVLVLCDGHSZTUAVIVLUBUCUDWDWGWEWDVNVKLZNWG
      ABVLDEFWIHUEWJVOVNVKUFUGUHCDVIBGHWHFUEUIUJUKZVOBDCIVOVKVJDIJZLZVQVOVKVNWL
      VOULVOVJVMDIVOVIVLVIVLKWKWKUMUNUOVJUPOBUPOZDUPOWMVQRVIVIWHWHUQFHVJBDURUSU
      HZUTUOWNAUPOVSVILFEBAVAUCWNCUPOVTWBLFGBCVAUCVBBVCOAVCOCVCOWAVPRBFTAETCGTB
      ACVDUSUHWOVEVRVJVMBDIVRVIVLVIVLKACBDIVFZWPUMVPVQVGUMVH $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( An ordered pair theorem for finite integers.  Analagous to ~ nn0opthi . $)
  omopth $p |- ( ( ( A e. om /\ B e. om ) /\ ( C e. om /\ D e. om ) ) ->
                 ( ( ( ( A +o B ) .o ( A +o B ) ) +o B ) =
                     ( ( ( C +o D ) .o ( C +o D ) ) +o D ) <->
                     ( A = C /\ B = D ) ) ) $=
    ( com wcel coa co comu wceq wa wb c0 cif opreq1 opreq12d opreq1d eqeq1d
    eqeq1 anbi1d bibi12d opreq2 id anbi2d eqeq2d eqeq2 peano1 elimel omopthi
    dedth4h ) AEFZBEFZCEFZDEFZABGHZUOIHZBGHZCDGHZURIHZDGHZJZACJZBDJZKZLUKAMNZBG
    HZVFIHZBGHZUTJZVECJZVCKZLVEULBMNZGHZVMIHZVLGHZUTJZVJVLDJZKZLVOUMCMNZDGHZVTI
    HZDGHZJZVEVSJZVQKZLVOVSUNDMNZGHZWGIHZWFGHZJZWDVLWFJZKZLABCDMMMMAVEJZVAVIVDV
    KWMUQVHUTWMUPVGBGWMUOVFUOVFIAVEBGOZWNPQRWMVBVJVCAVECSTUABVLJZVIVPVKVRWOVHVO
    UTWOVGVNBVLGWOVFVMVFVMIBVLVEGUBZWPPWOUCPRWOVCVQVJBVLDSUDUACVSJZVPWCVRWEWQUT
    WBVOWQUSWADGWQURVTURVTICVSDGOZWRPQUEWQVJWDVQCVSVEUFTUADWFJZWCWJWEWLWSWBWIVO
    WSWAWHDWFGWSVTWGVTWGIDWFVSGUBZWTPWSUCPUEWSVQWKWDDWFVLUFUDUAVEVLVSWFAMEUGUHB
    MEUGUHCMEUGUHDMEUGUHUIUJ $.
    $( [12-May-2012] $) $( [1-May-2012] $)

