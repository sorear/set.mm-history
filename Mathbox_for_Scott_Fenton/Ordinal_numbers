$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Ordinal numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z A $.
    $( A class of transitive sets is partially ordered by ` _E ` .
       (Contributed by Scott Fenton, 15-Oct-2010.) $)
    elpotr $p |- ( A. z e. A Tr z -> _E Po A ) $=
      ( vx vy wel wa wi wal wral wtr cep wpo alral alimi syl ralcom ralbii epel
      cv wbr ralimi bitri sylib dftr2 df-po anbi12i imbi12i elirrv mtbir bitr3i
      wn biantrur 2ralbii bitr4i 3imtr4i ) CDEZDAEZFZCAEZGZDHZCHZABIZUTABIZDBIZ
      CBIZASZJZABIBKLZVCUTDBIZCBIZABIZVFVBVKABVBVJCHVKVAVJCUTDBMNVJCBMOUAVLVJAB
      IZCBIVFVJACBBPVMVECBUTADBBPQUBUCVHVBABCDVGUDQVICSZVNKTZUKZVNDSZKTZVQVGKTZ
      FZVNVGKTZGZFZABIZDBICBIVFCDABKUEVDWDCDBBUTWCABUTWBWCVTURWAUSVRUPVSUQCDRDA
      RUFCARUGVPWBVOCCECUHCCRUIULUJQUMUNUO $.
      $( [15-Oct-2010] $)
  $}

  ${
    $( Given ~ ax-reg , an ordinal is a transitive class totally ordered by
       epsilon.  (Contributed by Scott Fenton, 28-Jan-2011.) $)
    dford3 $p |- ( Ord A <-> ( Tr A /\ _E Or A ) ) $=
      ( word wtr cep wwe wa wor df-ord wfr df-we zfregfr mpbiran anbi2i bitri )
      ABACZADEZFOADGZFAHPQOPADIQADJAKLMN $.
      $( [28-Jan-2011] $)
  $}

  ${
    $d x y $.  $d y ph $.  $d y ps $.
    $( Lemma for ~ dfon2 . $)
    dfon2lem1 $p |- Tr U. { x | ( ph /\ Tr x /\ ps ) } $=
      ( vy cv wtr w3a cab cuni truni wcel wsb hbs1 ax-17 hb3an vex sbequ12 treq
      weq 3anbi123d elabf simp2bi mprg ) DEZFZACEZFZBGZCHZIFDUIDUIJUDUIKACDLZUE
      BCDLZUHUJUEUKGCUDUJUEUKCACDMUECNBCDMODPCDSAUJUGUEBUKACDQUFUDRBCDQTUAUBUC
      $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d y ps $.
    $( Lemma for ~ dfon2 $)
    dfon2lem2 $p |- U. { x | ( x C_ A /\ ph /\ ps ) } C_ A $=
      ( cv wss w3a cab cpw cuni simp1 ss2abi df-pw sseqtr4i sspwuni mpbi ) CEDF
      ZABGZCHZDIZFSJDFSQCHTRQCQABKLCDMNSDOP $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d A x y z w t $.

    $( Lemma for ~ dfon2 .  All sets satisfying the new definition are
       transitive and untangled. $)
    dfon2lem3 $p |- ( A e. V -> ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) ->
                                        ( Tr A /\ A. z e. A -. z e. z ) ) ) $=
      ( vw vt vy wcel cv wtr wa wi wel wn wral wss w3a wceq treq cvv wal eluni2
      wpss cab cuni untelirr wrex vex sseq1 raleq 3anbi123d elequ1 elequ2 bitrd
      weq elab notbid cbvralv biimpi 3ad2ant3 sylbi ra4 rexlimiv mprg dfon2lem2
      syl dfpss2 dfon2lem1 ssexg mpan psseq1 anbi12d eleq1 imbi12d cla4gv sylan
      imp csuc csn biimpd pm2.43i cun df-suc sseq1i unss bitr4i biimpri sylancr
      snssg trsuc2 ax-mp untuni mprgbir ax-17 hbra1 hb3an untsucf sucexg cbvabv
      hbab hbuni eleq2i hbsuc raleqf elabg syl5bb biimprd com12 mp3an23 elssuni
      sucssel syl5 syld mpd syl6 mpan2i syl5bir mpani mt3i pm3.2i mpbii ex ) CD
      HZAIZCUCZYDJZKZYDCHZLZAUAZCJZBBMZNZBCOZKZYCYJKZEIZCPZYQJZFFMZNZFYQOZQZEUD
      ZUEZCRZYOYPUUFUUEUUEHZYMUUGNBUUEBUUEUFBIZUUEHBAMZAUUDUGYMAUUHUUDUBUUIYMAU
      UDYDUUDHZYMBYDOZUUIYMLUUJYDCPZYFUUAFYDOZQZUUKUUCUUNEYDAUHEAUOYRUULYSYFUUB
      UUMYQYDCUIYQYDSUUAFYQYDUJUKUPUUMUULUUKYFUUMUUKUUAYMFBYDFBUOZYTYLUUOYTBFMY
      LFBFULFBBUMUNUQURUSUTVAZYMBYDVBVFVCVAVDYPUUECPZUUFNZUUGYSUUBECVEZUUQUURKU
      UECUCZYPUUGUUECVGYPUUTUUEJZUUGYRUUBEVHZYPUUTUVAKZUUECHZUUGYCUUETHZYJUVCUV
      DLZUUQYCUVEUUSUUECDVIVJUVEYJUVFYIUVFAUUETYDUUERZYGUVCYHUVDUVGYEUUTYFUVAYD
      UUECVKYDUUESVLYDUUECVMVNVOVQVPUVDUUEVRZCPZUUGUVDUUQUUEVSZCPZUVIUUSUVDUVKU
      VDUVDUVKUUECCWIVTWAUVIUUQUVKKZUVIUUEUVJWBZCPUVLUVHUVMCUUEWCWDUUEUVJCWEWFW
      GWHUVDUVIUVHUUDHZUUGUVIUVDUVNUVIUVHJZUUAFUVHOZUVDUVNLUVAUVOUVBUUEWJWKYMBU
      UEOZUVPUVQUUKAUUDBAUUDWLUUPWMZBFUUEFBUUDUUCFEBYRYSUUBFYRFWNYSFWNUUAFYQWOW
      PZWTXAWQWKUVDUVIUVOUVPQZUVNUVDUVHTHZUVTUVNLUUECWRUWAUVNUVTUVNUVHUUHCPZUUH
      JZUUAFUUHOZQZBUDZHUWAUVTUUDUWFUVHUUCUWEEBYQUUHRYRUWBYSUWCUUBUWDYQUUHCUIYQ
      UUHSUUAFYQUUHUJUKWSXBUWEUVTBUVHTUUHUVHRUWBUVIUWCUVOUWDUVPUUHUVHCUIUUHUVHS
      UUAFGUUHUVHGIUUHHFWNFGUUEFGUUDUUCFEGUVSWTXAXCXDUKXEXFXGVFXHXIXHUVNUVHUUEP
      UVDUUGUVHUUDXJUUEUUECXKXLXMXNXOXPXQXRXSUUFUVAUVQKYOUVAUVQUVBUVRXTUUFUVAYK
      UVQYNUUECSYMBUUECUJVLYAVFYB $.
      $( [25-Feb-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    dfon2lem4.1 $e |- A e. _V $.
    dfon2lem4.2 $e |- B e. _V $.
    $( Lemma for ~ dfon2 .  If two sets satisfy the new definition, then one is
       a subset of the other. $)
    dfon2lem4 $p |- ( ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) /\
                         A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ->
                         ( A C_ B \/ B C_ A ) ) $=
      ( vz cv wpss wtr wa wcel wi wal wceq wo wss wn cvv eleq1 inss1 sseli wral
      cin wel dfon2lem3 ax-mp simprd eleq2 bitrd notbid rcla4cv syl adantr syl5
      pm2.01d elin notbii sylib simpld trin syl2an inex1 psseq1 anbi12d imbi12d
      treq cla4v mpan2d adantl anim12d mtod ianor sspss mpbi inss2 orc syl6 olc
      orel1 jaoa mp2ani df-ss sseqin2 orbi12i sylibr ) AHZCIZWGJZKZWGCLZMZANZBH
      ZDIZWNJZKZWNDLZMZBNZKZCDUDZCOZXBDOZPZCDQZDCQZPXAXBCIZRZXBDIZRZPZXEXAXHXJK
      ZRXLXAXMXBCLZXBDLZKZXAXBXBLZRZXPRXAXQXQXNXAXRXBCXBCDUAZUBWMXNXRMZWTWMGGUE
      ZRZGCUCZXTWMCJZYCCSLWMYDYCKMEAGCSUFUGZUHYBXRGXBCGHZXBOZYAXQYGYAXBYFLXQYFX
      BYFTYFXBXBUIUJUKULUMUNUOUPXQXPXBCDUQURUSXAXHXNXJXOXAXHXBJZXNWMYDDJZYHWTWM
      YDYCYEUTWTYIYBGDUCZDSLWTYIYJKMFBGDSUFUGUTCDVAVBZWMXHYHKZXNMZWTWLYMAXBCDEV
      CZWGXBOZWJYLWKXNYOWHXHWIYHWGXBCVDWGXBVGVEWGXBCTVFVHUNVIXAXJYHXOYKWTXJYHKZ
      XOMZWMWSYQBXBYNWNXBOZWQYPWRXOYRWOXJWPYHWNXBDVDWNXBVGVEWNXBDTVFVHVJVIVKVLX
      HXJVMUSXLXHXCPZXJXDPZXEXBCQYSXSXBCVNVOXBDQYTCDVPXBDVNVOXIYSXEXKYTXIYSXCXE
      XHXCVTXCXDVQVRXKYTXDXEXJXDVTXDXCVSVRWAWBUMXFXCXGXDCDWCDCWDWEWF $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z $.  $d B x y z $.
    dfon2lem5.1 $e |- A e. _V $.
    dfon2lem5.2 $e |- B e. _V $.
    $( Lemma for ~ dfon2 .  Two sets satisfying the new definition also satisfy
       trichotomy with respect to ` e. ` $)
    dfon2lem5 $p |- ( ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) /\
                         A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ->
                       ( A e. B \/ A = B \/ B e. A ) ) $=
      ( vz cv wpss wtr wa wcel wi wal wceq wn wo w3o wss cvv dfpss2 eqcom bitri
      dfon2lem4 notbii anbi2i orbi12i andir bitr4i orcom dfon2lem3 ax-mp simpld
      wel wral psseq1 treq anbi12d eleq1 imbi12d cla4v exp3a com23 sylan2 mpan9
      imp orim12d syl5bi syl5bir mpand 3orrot 3orass df-or 3bitri sylibr ) AHZC
      IZVPJZKZVPCLZMZANZBHZDIZWCJZKZWCDLZMZBNZKZCDOZPZDCLZCDLZQZMZWNWKWMRZWJCDS
      ZDCSZQZWLWOABCDEFUDWTWLKZCDIZDCIZQZWJWOXDWRWLKZWSWLKZQXAXBXEXCXFCDUAXCWSD
      COZPZKXFDCUAXHWLWSXGWKDCUBUEUFUCUGWRWSWLUHUIXDXCXBQWJWOXBXCUJWJXCWMXBWNWI
      WBDJZXCWMMZWIXIGGUNPZGDUOZDTLWIXIXLKMFBGDTUKULUMWBXIXJWBXCXIWMWBXCXIWMWAX
      CXIKZWMMADFVPDOZVSXMVTWMXNVQXCVRXIVPDCUPVPDUQURVPDCUSUTVAVBVCVFVDWBCJZWIX
      BWNMWBXOXKGCUOZCTLWBXOXPKMEAGCTUKULUMWIXBXOWNWIXBXOWNWHXBXOKZWNMBCEWCCOZW
      FXQWGWNXRWDXBWEXOWCCDUPWCCUQURWCCDUSUTVAVBVCVEVGVHVIVJWQWKWMWNRWKWOQWPWNW
      KWMVKWKWMWNVLWKWOVMVNVO $.
      $( [25-Feb-2011] $)
  $}

  ${
    $d S x y z w s t $.
    $( Lemma for ~ dfon2 .  A transitive class of sets satisfying the new
       definition satisfies the new definition. $)
    dfon2lem6 $p |- ( ( Tr S /\
                         A. x e. S A. z ( ( z C. x /\ Tr z ) -> z e. x ) ) ->
                       A. y ( ( y C. S /\ Tr y ) -> y e. S ) ) $=
      ( vs vw vt wtr cv wpss wa wel wi wal wral wcel weq wn syl imbi12d wo cdif
      pssss ssralv impcom adantrr ad2ant2lr psseq2 anbi1d elequ2 albidv rcla4cv
      wss imp eldifi rcla4v psseq1 anbi12d elequ1 cbvalv syl6ib ad2ant2l adantr
      treq w3o vex dfon2lem5 3orrot 3orass bitri eleq1a elndif nsyli orel1 trss
      adantll eldifn ssel con3d syl5com syl9 adantl imp31 syl9r mpd syl5bi syl5
      mp2and ex ssrdv dfpss2 exp3a com23 syl6 com3l adantld imp32 syl5bir mpand
      a4v orrd anassrs ralrimiva wrex wne df-pss pssdifn0 sylbi r19.2z ad2antrl
      c0 eleq1 syl5ibr a1i trel syl7 ad2antrr jaod rexlimdv syld alrimiv ) DHZC
      IZAIZJZYCHZKZCALZMZCNZADOZKZBIZDJZYMHZKZYMDPZMBYLYPYQYLYPKZBEQZBELZUAZEDY
      MUBZOZYQYRUUAEUUBYLYPEIZUUBPZUUAYLYPUUEKZKZYSYTUUGYMUUDUMZYSRZYTUUGFYMUUD
      UUGFBLZFELZUUGUUJKZYCFIZJZYFKZCFLZMZCNZGIZUUDJZUUSHZKZGELZMZGNZUUKUUGUUJU
      URUUGYJAYMOZUUJUURMYKYPUVFYBUUEYKYNUVFYOYNYKUVFYNYMDUMZYKUVFMYMDUCYJAYMDU
      DSUEUFUGYJUURAUUMYMAFQZYIUUQCUVHYGUUOYHUUPUVHYEUUNYFYDUUMYCUHUIAFCUJTUKUL
      SUNUUGUVEUUJYKUUEUVEYBYPUUEYKUVEUUEYKYCUUDJZYFKZCELZMZCNZUVEUUEUUDDPZYKUV
      MMUUDDYMUOZYJUVMAUUDDAEQZYIUVLCUVPYGUVJYHUVKUVPYEUVIYFYDUUDYCUHUIAECUJTUK
      UPSZUVLUVDCGCGQZUVJUVBUVKUVCUVRUVIUUTYFUVAYCUUSUUDUQYCUUSVDURCGEUSTUTVAUE
      VBVCUURUVEKUUKFEQZEFLZVEZUULUUKCGUUMUUDFVFEVFVGUWAUVSUVTUUKUAZUAZUULUUKUW
      AUVSUVTUUKVEUWCUUKUVSUVTVHUVSUVTUUKVIVJUULUVSRZUWCUUKMUUFUUJUWDYLUUEUUJUW
      DYPUUEUUJUWDUUEUVSUUMUUBPUUJUUDUUBUUMVKUUMYMDVLVMUNVPVPUWDUWCUWBUULUUKUVS
      UWBVNUULUVTRZUWBUUKMUUFUUJUWEYLYPUUEUUJUWEYOUUEUUJUWEMMYNYOUUJUUMYMUMZUUE
      UWEYMUUMVOUUEEBLZRUWFUWEUUDDYMVQUWFUVTUWGUUMYMUUDVRVSVTWAWBWCVPUVTUUKVNSW
      DWEWFWGWHWIWJUUHUUIKYMUUDJZUUGYTYMUUDWKYLYPUUEUWHYTMZYKYPUUEUWIMZMYBYKYOU
      WJYNUUEYKYOUWIUUEYKUVMYOUWIMUVQUVMUWHYOYTUVMUWHYOYTUVLUWHYOKZYTMCBCBQZUVJ
      UWKUVKYTUWLUVIUWHYFYOYCYMUUDUQYCYMVDURCBEUSTWTWLWMWNWOWPWBWQWRWSXAXBXCYRU
      UCUUAEUUBXDZYQYNUUCUWMMZYLYOYNUUBXKXEZUWNYNUVGYMDXEKUWOYMDXFYMDXGXHUWOUUC
      UWMUUAEUUBXIWISXJYRUUAYQEUUBYRUUAUUEYQYRYSUUEYQMZYTYSUWPMYRUUEYQYSUVNUVOY
      MUUDDXLXMXNYBYTUWPMYKYPUUEUVNYBYTYQUVOYBYTUVNYQDYMUUDXOWLXPXQXRWMXSXTWEWI
      YA $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z w s t u $.  $d B x y z w t $.
    dfon2lem7.1 $e |- A e. _V $.
    $( Lemma for ~ dfon2 .  All elements of a new ordinal are new ordinals. $)
    dfon2lem7 $p |- ( A. x ( ( x C. A /\ Tr x ) -> x e. A ) ->
                       ( B e. A -> A. y ( ( y C. B /\ Tr y ) -> y e. B ) ) ) $=
      ( vw vt vu vz wpss wtr wa wcel wi wal wss wel wral sylbi imbi12d w3a cuni
      vs cv cab wceq csuc csn wn weq elequ1 elequ2 notbid cbvralv biimpi ralimi
      bitrd untuni sylibr vex sseq1 treq raleq 3anbi123d dfon2lem3 ax-mp simprd
      elab cvv untelirr 3ad2ant3 mprg psseq2 anbi1d albidv 3anbi3i abbii unieqi
      syl eleq2i notbii sylib dfon2lem2 snss mtbi intnan cun df-suc sseq1i unss
      ssexi bitr4i mtbir dfon2lem1 trsuc2 elsuc wrex eluni2 hba1 rcla4cv psseq1
      wo anbi12d cbvalv syl6ib rexlimi rexlimiv rgen dfon2lem6 mp2an eleq2 jaoi
      mpbiri sucex elssuni sylbir mp3an23 ralbii syl6bb cbvabv syl6sseq syl5bir
      mpani syl5bi mtoi eleq1 cla4v mpan2i mtod dfpss2 biimpri mpan nsyl2 mpbii
      a1i ra4 ) AUDZCJZYQKZLZYQCMZNZAOZFUDZCPZUUDKZBUDZGUDZJZUUGKZLZBGQZNZBOZGU
      UDRZUAZFUEZUBZCUFZDCMUUGDJZUUJLZUUGDMZNZBOZNZUUCUURCJZUUSUUCUVFUURCMZUUCU
      VGUURUGZUUEUUFUUGHUDZJZUUJLZBHQZNZBOZHUUDRZUAZFUEZUBZPZUVSUURUVRPZUURUHZU
      VRPZLZUWBUVTUURUVRMZUWBIIQZUIZIUURRZUWDUIZGGQZUIZGYQRZUWGAUUQUWKAUUQRUWFI
      YQRZAUUQRUWGUWKUWLAUUQUWKUWLUWJUWFGIYQGIUJZUWIUWEUWMUWIIGQUWEGIGUKGIIULUQ
      UMUNUOUPIAUUQURUSYQUUQMZYQCPZYSUUNGYQRZUAZUWKUUPUWQFYQAUTZFAUJZUUEUWOUUFY
      SUUOUWPUUDYQCVAZUUDYQVBZUUNGUUDYQVCZVDVHZUWPUWOUWKYSUUNUWJGYQUUNHHQUIHUUH
      RZUWJUUNUUHKZUXDUUHVIMUUNUXEUXDLNGUTBHUUHVIVEVFVGHUUHVJVSUPVKSVLUWGUURUUR
      MZUIUWHIUURVJUXFUWDUURUVRUURUUQUVQUUPUVPFUUOUVOUUEUUFUUNUVNGHUUDGHUJZUUMU
      VMBUXGUUKUVKUULUVLUXGUUIUVJUUJUUHUVIUUGVMVNGHBULTVOZUNVPVQVRVTWAWBVFUURUV
      RUURCEUUFUUOFCWCZWKZWDWEWFUVSUURUWAWGZUVRPUWCUVHUXKUVRUURWHZWIUURUWAUVRWJ
      WLWMUVGUWACPZUUCUVSUURCUXJWDUUCUURCPZUXMUVSUXIUXNUXMLZUVHCPZUUCUVSUXPUXKC
      PUXOUVHUXKCUXLWIUURUWACWJWLUXPUVSNUUCUXPUVHUCUDZCPZUXQKZYQUVIJZYSLZAHQZNZ
      AOZHUXQRZUAZUCUEZUBZUVRUXPUVHKZUYDHUVHRZUVHUYHPZUURKZUYIUUEUUOFWNZUURWOVF
      UYDHUVHUVIUVHMUVIUURMZUVIUURUFZXBUYDUVIUURHUTWPUYNUYDUYOUYNHAQZAUUQWQZUYD
      AUVIUUQWRZUYPUYDAUUQUYCAWSUWNUWQUYPUYDNZUXCUWPUWOUYSYSUWPUYPUVNUYDUUNUVNG
      UVIYQUXHWTZUVMUYCBABAUJZUVKUYAUVLUYBVUAUVJUXTUUJYSUUGYQUVIXAUUGYQVBXCBAHU
      KTXDXEVKSXFSUYOUYDYQUURJZYSLZYQUURMZNZAOZUYLIUDZUVIJZVUGKZLZIHQZNZIOZHUUR
      RVUFUYMVUMHUURUYNUYQVUMUYRUYPVUMAUUQUWNUWQUYPVUMNZUXCUWPUWOVUNYSUWPUYPUVN
      VUMUYTUVMVULBIBIUJZUVKVUJUVLVUKVUOUVJVUHUUJVUIUUGVUGUVIXAUUGVUGVBXCBIHUKT
      XDXEVKSXGSXHHAIUURXIXJUYOUYCVUEAUYOUYAVUCUYBVUDUYOUXTVUBYSUVIUURYQVMVNUVI
      UURYQXKTVOXMXLSXHUXPUYIUYJUAZUVHUYGMUYKUYFVUPUCUVHUURUXJXNUXQUVHUFUXRUXPU
      XSUYIUYEUYJUXQUVHCVAUXQUVHVBUYDHUXQUVHVCVDVHUVHUYGXOXPXQUYGUVQUYFUVPUCFUC
      FUJZUXRUUEUXSUUFUYEUVOUXQUUDCVAUXQUUDVBVUQUYEUYDHUUDRUVOUYDHUXQUUDVCUYDUV
      NHUUDUYCUVMABABUJZUYAUVKUYBUVLVURUXTUVJYSUUJYQUUGUVIXAYQUUGVBXCABHUKTXDXR
      XSVDXTVRYAYOYBYCYDYEUUCUVFUYLUVGUYMUUBUVFUYLLZUVGNAUURUXJYQUURUFZYTVUSUUA
      UVGVUTYRUVFYSUYLYQUURCXAYQUURVBXCYQUURCYFTYGYHYIUXNUUSUIZUVFUXIUVFUXNVVAL
      UURCYJYKYLYMUUSUUGVUGJZUUJLZBIQZNZBOZICRZUVEUUSVVFIUURRVVGVVFIUURVUGUURMI
      AQZAUUQWQVVFAVUGUUQWRVVHVVFAUUQUWNUWOYSVVFIYQRZUAZVVHVVFNZUUPVVJFYQUWRUWS
      UUEUWOUUFYSUUOVVIUWTUXAUWSUUOUWPVVIUXBUUNVVFGIYQUWMUUMVVEBUWMUUKVVCUULVVD
      UWMUUIVVBUUJUUHVUGUUGVMVNGIBULTVOUNXSVDVHVVIUWOVVKYSVVFIYQYPVKSXGSXHVVFIU
      URCVCYNVVFUVDIDCVUGDUFZVVEUVCBVVLVVCUVAVVDUVBVVLVVBUUTUUJVUGDUUGVMVNVUGDU
      UGXKTVOWTVSVS $.
      $( [25-Feb-2011] $)
  $}


  ${
    $d A x y z w s t $.
    $( Lemma for ~ dfon2 .  The intersection of a non-empty class ` A ` of new
       ordinals is itself a new ordinal and is contained within ` A ` $)
    dfon2lem8 $p |- ( ( A =/= (/) /\
                         A. x e. A A. y ( ( y C. x /\ Tr y ) -> y e. x ) ) ->
                       ( A. z ( ( z C. |^| A /\ Tr z ) -> z e. |^| A ) /\
                         |^| A e. A ) ) $=
      ( vt vw cv wpss wtr wa wel wi wal wral wcel wn cvv syl adantl sylbi ax-mp
      c0 wne cint dfon2lem3 simpld ralimi trint dfon2lem7 alrimiv df-ral 19.21v
      vex cuni albii bitr4i impexp 2albii wrex eluni2 biimpi imim1i alimi alcom
      wex 19.23v df-rex imbi1i bitri 3imtr4i sylbir wss ssralv adantr dfon2lem6
      intssuni mpd intex imp simprd untelirr adantlr risset notbii ralnex eqcom
      wceq weq psseq2 anbi1d elequ2 imbi12d albidv rcla4cv intss1 dfpss2 psseq1
      treq anbi12d eleq1 cla4gv exp3a syl5bir exp4b com45 syl5 mpdd mpid syl7bi
      com23 ralrimiv ralim syl5bi wb elintg ad2antrr sylibrd mt3d ancld mp2and
      ex ) DUBUCZBGZAGZHZYCIZJZBAKZLZBMZADNZJZDUDZIZEGZFGZHYOIJEFKLEMZFYMNZCGZY
      MHYSIJYSYMOLCMZYMDOZJZYKYNYBYKYDIZADNYNYJUUCADYJUUCCCKPCYDNZYDQOYJUUCUUDJ
      LAUMZBCYDQUEUAUFUGADUHRSYLYQFDUNZNZYRYKUUGYBYKFAKZYQLZFMZADNZUUGYJUUJADYJ
      UUIFBEYDYPUUEUIUJUGUUKYDDOZUUILZFMZAMZUUGUUKUULUUJLZAMUUOUUJADUKUUNUUPAUU
      LUUIFULUOUPUUOUULUUHJZYQLZFMAMZUUGUURUUMAFUULUUHYQUQURUUHADUSZYQLZFMZYPUU
      FOZYQLZFMUUSUUGUVAUVDFUVCUUTYQUVCUUTAYPDUTVAVBVCUUSUURAMZFMUVBUURAFVDUVEU
      VAFUVEUUQAVEZYQLUVAUUQYQAVFUUTUVFYQUUHADVGVHUPUOVIYQFUUFUKVJVKTRSYBUUGYRL
      ZYKYBYMUUFVLUVGDVPYQFYMUUFVMRVNVQYNYRJYTYLUUBFCEYMVOYLYTUUAYLYTUUAYLYTJZU
      UAYMYMOZYBYTUVIPZYKYBYTJZEEKPEYMNZUVJUVKYNUVLYBYTYNUVLJZYBYMQOZYTUVMLDVRZ
      CEYMQUETVSZVTEYMWARWBUVHUUAPZYMYOOZEDNZUVIUVQYOYMWGZPZEDNZUVHUVSUVQUVTEDU
      SZPUWBUUAUWCEYMDWCWDUVTEDWEUPUVHUWAUVRLZEDNUWBUVSLUVHUWDEDUWAYMYOWGZPZUVH
      YODOZUVRUVTUWEYOYMWFWDUVHUWGYNUWFUVRLZYBYTYNYKUVKYNUVLUVPUFWBYLUWGYNUWHLZ
      LYTYLUWGYCYOHZYFJZBEKZLZBMZUWIYKUWGUWNLYBYJUWNAYODAEWHZYIUWMBUWOYGUWKYHUW
      LUWOYEUWJYFYDYOYCWIWJAEBWKWLWMWNSYBUWGUWNUWILZLYKUWGYMYOVLZYBUWPYODWOYBUW
      NUWQUWIYBUWNUWQUWFYNUVRYBUWNUWQUWFYNUVRLZUWQUWFJYMYOHZYBUWNJZUWRYMYOWPUWT
      UWSYNUVRYBUWNUWSYNJZUVRLZYBUVNUWNUXBLUVOUWMUXBBYMQYCYMWGZUWKUXAUWLUVRUXCU
      WJUWSYFYNYCYMYOWQYCYMWRWSYCYMYOWTWLXATVSXBXCXDXEXJXFVNXGVNXHXIXKUWAUVREDX
      LRXMYBUVIUVSXNZYKYTYBUVNUXDUVOEYMDQXOTXPXQXRYAXSXFXT $.
      $( [26-Feb-2011] $)
  $}

  ${
    $d A x y z w s t u v $.
    $( Lemma for ~ dfon2 .  A class of new ordinals is well-founded by
       ` _E ` . $)
    dfon2lem9 $p |- ( A. x e. A A. y ( ( y C. x /\ Tr y ) -> y e. x )
                         -> _E Fr A ) $=
      ( vz vt vw vu cv wpss wtr wa wel wi wal wral wss wn wrex cep wcel wne wfr
      c0 ssralv cint dfon2lem8 simprd intss1 simpld cvv wceq dfon2lem3 untelirr
      intex imp eleq1 notbid syl5ibcom a1dd trss eqss biimpri expcom syl6 com23
      syl con3 pm2.61d sylanb syldan syl5 eleq2 ralbidv rcla4ev syl2anc syl6com
      ralrimiv imp3a alrimiv df-fr epel notbii ralbii rexbii imbi2i albii bitri
      wbr sylibr ) BHZAHIWJJKBALMBNZACOZDHZCPZWMUCUAZKZEFLZQZEWMOZFWMRZMZDNZCSU
      BZWLXADWLWNWOWTWNWLWKAWMOZWOWTMWKAWMCUDWOXDWTWOXDKZWMUEZWMTZEHZXFTZQZEWMO
      ZWTXEGHZXFIXLJKXLXFTMGNZXGABGWMUFZUGXEXJEWMEDLXFXHPZXEXJXHWMUHWOXDXMXOXJM
      ZXEXMXGXNUIWOXFUJTZXMXPWMUNXQXMKZXFXHUKZXPXRXSXJXOXRXFXFTZQZXSXJXRAALQAXF
      OZYAXRXFJZYBXQXMYCYBKGAXFUJULUOZUGAXFUMVFXSXTXIXFXHXFUPUQURUSXRXOXSQZXJXR
      XOXIXSMYEXJMXRXIXOXSXRXIXHXFPZXOXSMXRYCXIYFMXRYCYBYDUIXFXHUTVFXOYFXSXSXOY
      FKXFXHVAVBVCVDVEXIXSVGVDVEVHVIVJVKVQWSXKFXFWMFHZXFUKZWRXJEWMYHWQXIYGXFXHV
      LUQVMVNVOVCVPVRVSXCWPXHYGSWHZQZEWMOZFWMRZMZDNXBDFECSVTYMXADYLWTWPYKWSFWMY
      JWREWMYIWQEFWAWBWCWDWEWFWGWI $.
      $( [3-Mar-2011] $)
  $}

  ${
    $d x y z w t u v $.
    $( ` On ` consists of all sets that contain all its transitive proper
       subsets.  This definition comes from J. R. Isbell, "A Definition of
       Ordinal Numbers," American Mathematical Monthly, vol 67 (1960), pp.
       51-52.  (Contributed by Scott Fenton, 20-Feb-2011.) $)
    dfon2 $p |- On = { x | A. y ( ( y C. x /\ Tr y ) -> y e. x ) } $=
      ( vz vw vu vt vv cv cab wpss wtr wa wel wi wal cep wral vex weq imbi12d
      con0 df-on wss wne tz7.7 df-pss syl6bbr exbiri com23 imp3a alrimiv wwe wn
      word cvv wcel dfon2lem3 ax-mp simpld wfr w3o dfon2lem7 ralrimiv dfon2lem9
      psseq2 anbi1d elequ2 albidv psseq1 anbi12d elequ1 cbvalv syl6bb dfon2lem5
      treq rcla4cv anim12d syl6 ralrimivv jca syl wbr dfwe2 epel biid 3orbi123i
      2ralbii anbi2i bitri sylibr df-ord sylanbrc impbii abbii eqtri ) UAAHZUNZ
      AIBHZWPJZWRKZLBAMZNZBOZAIAUBWQXCAWQXCWQXBBWQWSWTXAWQWTWSXAWQWTXAWSWQWTLXA
      WRWPUCWRWPUDLWSWPWRUEWRWPUFUGUHUIUJUKXCWPKZWPPULZWQXCXDCCMUMCWPQZWPUOUPXC
      XDXFLNARZBCWPUOUQURUSXCWPPUTZCDMZCDSZDCMZVAZDWPQCWPQZLZXEXCEHZFHZJZXOKZLZ
      EFMZNZEOZFWPQZXNXCYBFWPBEWPXPXGVBVCYCXHXMFEWPVDYCXLCDWPWPYCCAMZDAMZLGHZCH
      ZJZYFKZLZGCMZNZGOZWRDHZJZWTLZBDMZNZBOZLXLYCYDYMYEYSYBYMFYGWPFCSZYBXOYGJZX
      RLZECMZNZEOYMYTYAUUDEYTXSUUBXTUUCYTXQUUAXRXPYGXOVEVFFCEVGTVHUUDYLEGEGSZUU
      BYJUUCYKUUEUUAYHXRYIXOYFYGVIXOYFVOVJEGCVKTVLVMVPYBYSFYNWPFDSZYBXOYNJZXRLZ
      EDMZNZEOYSUUFYAUUJEUUFXSUUHXTUUIUUFXQUUGXRXPYNXOVEVFFDEVGTVHUUJYREBEBSZUU
      HYPUUIYQUUKUUGYOXRWTXOWRYNVIXOWRVOVJEBDVKTVLVMVPVQGBYGYNCRDRVNVRVSVTWAXEX
      HYGYNPWBZXJYNYGPWBZVAZDWPQCWPQZLXNCDWPPWCUUOXMXHUUNXLCDWPWPUULXIXJXJUUMXK
      CDWDXJWEDCWDWFWGWHWIWJWPWKWLWMWNWO $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d x y $.
    $( The domain of the epsilon relation is the universe.  (Contributed by
       Scott Fenton, 27-Oct-2010.) $)
    domep $p |- dom _E = _V $=
      ( vx vy cep cdm cv wbr wex cab cvv df-dm wceq df-v equid wcel exbii mpbir
      el epel 2th abbii eqtri eqtr4i ) CDAEZBEZCFZBGZAHZIABCJIUCUCKZAHUGALUHUFA
      UHUFAMUFUCUDNZBGABQUEUIBABROPSTUAUB $.
      $( [27-Oct-2010] $)
  $}

  $( Ordinal two is not zero.  (Contributed by Scott Fenton, 17-Jun-2011.) $)
  2on0 $p |- 2o =/= (/) $=
    ( c2o c1o csuc c0 df-2o nsuceq0 eqnetri ) ABCDEBFG $.
    $( [10-Jul-2011] $) $( [17-Jun-2011] $)

  ${
    $d A x $.  $d B x $.
    $( Given an element ` A ` of the union of an ordinal ` B ` , ` suc A ` is
       an element of ` B ` itself.  (Contributed by Scott Fenton,
       28-Mar-2012.) $)
    ordsucuniel $p |- ( Ord B -> ( A e. U. B <-> suc A e. B ) ) $=
      ( vx word cuni wcel csuc c0 wne wa ne0i sylbi anim2i wceq con0 syl bibi1d
      wb eleq2 sylc cv wex eluni adantl exlimiv wrex wlim wo wn wi df-ne ordzsl
      w3o 3orel1 syl5bi impcom eloni ordsucelsuc ordunisuc unieq eleq2d bibi12d
      biimprcd syl5ibrcom rexlimiv limsuc limuni biimpcd jaoi pm5.21nd ) BDZABE
      ZFZAGZBFZVKBHIZJZVMVPVKVMACUAZFZVRBFZJZCUBVPCABUCWAVPCVTVPVSBVRKUDUELMVOV
      PVKBVNKMVQBVRGZNZCOUFZBUGZUHZVMVORZVPVKWFVPBHNZUIZVKWFUJBHUKVKWHWDWEUMWIW
      FCBULWHWDWEUNUOLUPWDWGWEWCWGCOVROFZWGWCAWBEZFZVNWBFZRZWJVSWMRZWKVRNZWNWJV
      RDZWOVRUQZAVRURPWJWQWPWRVRUSPWPWNWOWPWLVSWMWKVRASQVCTWCVMWLVOWMWCVLWKABWB
      UTVABWBVNSVBVDVEWEABFZVORZBVLNZWGBAVFBVGXAWTWGXAWSVMVOBVLASQVHTVIPVJ $.
      $( [11-Apr-2012] $) $( [28-Mar-2012] $)
  $}

  ${
    nncli.1 $e |- A e. om $.
    nncli.2 $e |- B e. om $.
    $( ` om ` is closed under addition.  Inference form of ~ nnacl .
       (Contributed by Scott Fenton, 20-Apr-2012.) $)
    nnacli $p |- ( A +o B ) e. om $=
      ( com wcel coa co nnacl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [12-May-2012] $) $( [20-Apr-2012] $)

    $( ` om ` is closed under multiplication.  Inference form of ~ nnmcl .
       (Contributed by Scott Fenton, 20-Apr-2012.) $)
    nnmcli $p |- ( A .o B ) e. om $=
      ( com wcel comu co nnmcl mp2an ) AEFBEFABGHEFCDABIJ $.
      $( [12-May-2012] $) $( [20-Apr-2012] $)

  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    $( Adding to both sides of an inequality in ` om ` (Contributed by Scott
       Fenton, 16-Apr-2012.) $)
    omssadd $p |- ( ( A e. om /\ B e. om /\ C e. om ) ->
             ( A C_ B -> ( A +o C ) C_ ( B +o C ) ) ) $=
      ( vx com wcel wss co wi wa c0 wceq opreq2 sseq12d imbi2d con0 nnon ancoms
      coa syl vy cv csuc weq adantr adantl biimprd syl2an word wb nnacl adantrr
      oa0 eloni adantrl ordsucsssuc syl2anc biimpa nnasuc mpbird ex finds com12
      imim2d a2d 3impia ) AEFZBEFZCEFZABGZACSHZBCSHZGZIZVIVGVHJZVNVOVJADUBZSHZB
      VPSHZGZIZIVOVJAKSHZBKSHZGZIZIVOVJAUAUBZSHZBWESHZGZIZIVOVJAWEUCZSHZBWJSHZG
      ZIZIVOVNIDUACVPKLZVTWDVOWOVSWCVJWOVQWAVRWBVPKASMVPKBSMNOODUAUDZVTWIVOWPVS
      WHVJWPVQWFVRWGVPWEASMVPWEBSMNOOVPWJLZVTWNVOWQVSWMVJWQVQWKVRWLVPWJASMVPWJB
      SMNOOVPCLZVTVNVOWRVSVMVJWRVQVKVRVLVPCASMVPCBSMNOOVGAPFZBPFZWDVHAQBQWSWTJZ
      WCVJXAWAAWBBWSWAALWTAUMUEWTWBBLWSBUMUFNUGUHWEEFZVOWIWNXBVOWIWNIXBVOJZWHWM
      VJXCWHWMXCWHJWMWFUCZWGUCZGZXCWHXFXCWFUIZWGUIZWHXFUJXCWFPFZXGXCWFEFZXIXBVG
      XJVHVGXBXJAWEUKRULWFQTWFUNTXCWGPFZXHXCWGEFZXKXBVHXLVGVHXBXLBWEUKRUOWGQTWG
      UNTWFWGUPUQURXCWMXFUJWHXCWKXDWLXEXBVGWKXDLZVHVGXBXMAWEUSRULXBVHWLXELZVGVH
      XBXNBWEUSRUONUEUTVAVDVAVEVBVCVF $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( Multiply an ordinal by ` 2o ` (Contributed by Scott Fenton,
     18-Apr-2012.) $)
  onm2 $p |- ( A e. On -> ( A .o 2o ) = ( A +o A ) ) $=
    ( con0 wcel c2o comu co c1o csuc coa df-2o opreq2i wceq 1on omsuc mpan2 om1
    opreq1d eqtrd syl5eq ) ABCZADEFAGHZEFZAAIFZDUAAEJKTUBAGEFZAIFZUCTGBCUBUELMA
    GNOTUDAAIAPQRS $.
    $( [12-May-2012] $) $( [18-Apr-2012] $)

  $( Multiply an element of ` om ` by ` 2o ` (Contributed by Scott Fenton,
     16-Apr-2012.) $)
  omm2 $p |- ( A e. om -> ( 2o .o A ) = ( A +o A ) ) $=
    ( com wcel c2o comu co coa wceq 2onn nnmcom mpan con0 nnon onm2 syl eqtrd )
    ABCZDAEFZADEFZAAGFZDBCQRSHIDAJKQALCSTHAMANOP $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    omopthlem1.1 $e |- A e. om $.
    omopthlem1.2 $e |- C e. om $.
    $( Lemma for ~ omopthi . $)
    omopthlem1 $p |- ( A e. C ->
                ( ( A .o A ) +o ( A .o 2o ) ) e. ( C .o C ) ) $=
      ( csuc wss comu co wcel c2o coa con0 wi com ax-mp nnoni mp3an nnmcli wceq
      mp2an peano2 omwordi omwordri onsucssi nnacli nnasuc nnmsuc nnaass nnmcom
      sstrd 2onn eqtri opreq1i onm2 opreq2i 3eqtr4ri suceq sseq1i bitri 3imtr4i
      ) AEZBFZVAVAGHZBBGHZFZABIAAGHZAJGHZKHZVDIZVBVCVABGHZVDVALIZBLIZVKVBVCVJFM
      VAANIZVANIZCAUAOZPZBDPZVPVABVAUBQVKVLVLVBVJVDFMVPVQVQVABBUCQUJABACPZVQUDV
      IVHEZVDFVEVHVDVHVFVGAACCRZAJCUKRUEPVDBBDDRPUDVSVCVDVAAGHZVAKHZWAAKHZEZVCV
      SWANIVMWBWDSVAAVOCRCWAAUFTVNVMVCWBSVOCVAAUGTVHWCSVSWDSVFAKHZAKHZVFAAKHZKH
      ZWCVHVFNIVMVMWFWHSVTCCVFAAUHQWAWEAKWAAVAGHZWEVNVMWAWISVOCVAAUITVMVMWIWESC
      CAAUGTULUMVGWGVFKALIVGWGSVRAUNOUOUPVHWCUQOUPURUSUT $.
      $( [12-May-2012] $) $( [18-Apr-2012] $)


  $}

  ${
    omopthlem2.1 $e |- A e. om $.
    omopthlem2.2 $e |- B e. om $.
    omopthlem2.3 $e |- C e. om $.
    omopthlem2.4 $e |- D e. om $.
    $( Lemma for ~ omopthi . $)
    omopthlem2 $p |- ( ( A +o B ) e. C ->
     -. ( ( C .o C ) +o D ) = ( ( ( A +o B ) .o ( A +o B ) ) +o B ) ) $=
      ( coa co wcel comu wceq wss com nnacli mp2an nnacom con0 nnoni wn sseqtri
      c2o nnaword1 onm2 nnaass mp3an syl6eqr ax-mp sseqtr4i 2onn nnmcli omssadd
      wi 3sstr4i omopthlem1 ontr2 sylancr sseli onirri eleq1 mtbii con2i 3syl
      wa ) ABIJZCKZVFVFLJZBIJZCCLJZKZVIVJDIJZKZVLVIMZUAVGVIVHVFUCLJZIJZNZVPVJKZ
      VKBVHIJZVOVHIJZVIVPBVONZVSVTNZBVFAIJZBIJZVOBBWCIJZWDBOKZWCOKZBWENFVFAABEF
      PZEPZBWCUDQWFWGWEWDMFWIBWCRQUBVFSKZVOWDMVFWHTWJVOVFVFIJZWDVFUEVFOKAOKWFWD
      WKMWHEFVFABUFUGUHUIUJWFVOOKZVHOKZWAWBUNFVFUCWHUKULZVFVFWHWHULZBVOVHUMUGUI
      WMWFVIVSMWOFVHBRQWMWLVPVTMWOWNVHVORQUOVFCWHGUPVISKVJSKVQVRVEVKUNVIVHBWOFP
      TVJCCGGULZTVIVPVJUQQURVJVLVIVJOKDOKVJVLNWPHVJDUDQUSVNVMVNVLVLKVMVLVLVJDWP
      HPTUTVLVIVLVAVBVCVD $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    omopth.1 $e |- A e. om $.
    omopth.2 $e |- B e. om $.
    omopth.3 $e |- C e. om $.
    omopth.4 $e |- D e. om $.
    $( An ordered pair theorem for ` om ` .  Theorem 17.3 of [Quine] p. 124.
       This proof is adapted from ~ nn0opthi .  (Contributed by Scott Fenton,
       16-Apr-2012.) $)
    omopthi $p |- ( ( ( ( A +o B ) .o ( A +o B ) ) +o B ) =
                     ( ( ( C +o D ) .o ( C +o D ) ) +o D ) <->
                     ( A = C /\ B = D ) ) $=
      ( coa co comu wceq wn wcel wb nnoni mp2an sylib opreq12d com wa wo nnacli
      word onordi ordtri3 con2bii omopthlem2 eqcom notbii jaoi con4i id opreq1d
      sylbir eqtr4d nnmcli nnacan mp3an opreq2d nnacom 3eqtr4g con0 jca opreq12
      oacan simpr impbii ) ABIJZVIKJZBIJZCDIJZVLKJZDIJZLZACLZBDLZUAZVOVPVQVOBAI
      JZBCIJZLZVPVOVICBIJZVSVTVOVIVLWBVIVLLZVOWCMVIVLNZVLVINZUBZVOMZWCWFVIUDVLU
      DWCWFMOVIVIABEFUCZPUEVLVLCDGHUCZPUEVIVLUFQUGWDWGWEWDVNVKLZMWGABVLDEFWIHUH
      WJVOVNVKUIUJRCDVIBGHWHFUHUKUOULZVOBDCIVOVKVJDIJZLZVQVOVKVNWLVOUMVOVJVMDIV
      OVIVLVIVLKWKWKSUNUPVJTNBTNZDTNWMVQOVIVIWHWHUQFHVJBDURUSRZUTUPWNATNVSVILFE
      BAVAQWNCTNVTWBLFGBCVAQVBBVCNAVCNCVCNWAVPOBFPAEPCGPBACVFUSRWOVDVRVJVMBDIVR
      VIVLVIVLKACBDIVEZWPSVPVQVGSVH $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( An ordered pair theorem for finite integers.  Analagous to ~ nn0opthi .
     (Contributed by Scott Fenton, 1-May-2012.) $)
  omopth $p |- ( ( ( A e. om /\ B e. om ) /\ ( C e. om /\ D e. om ) ) ->
                 ( ( ( ( A +o B ) .o ( A +o B ) ) +o B ) =
                     ( ( ( C +o D ) .o ( C +o D ) ) +o D ) <->
                     ( A = C /\ B = D ) ) ) $=
    ( com wcel coa co comu wceq wa wb c0 opreq1 opreq12d opreq1d bibi12d peano1
    cif elimel eqeq1d eqeq1 anbi1d opreq2 anbi2d eqeq2d eqeq2 omopthi dedth4h
    id ) AEFZBEFZCEFZDEFZABGHZUOIHZBGHZCDGHZURIHZDGHZJZACJZBDJZKZLUKAMSZBGHZVFI
    HZBGHZUTJZVECJZVCKZLVEULBMSZGHZVMIHZVLGHZUTJZVJVLDJZKZLVOUMCMSZDGHZVTIHZDGH
    ZJZVEVSJZVQKZLVOVSUNDMSZGHZWGIHZWFGHZJZWDVLWFJZKZLABCDMMMMAVEJZVAVIVDVKWMUQ
    VHUTWMUPVGBGWMUOVFUOVFIAVEBGNZWNOPUAWMVBVJVCAVECUBUCQBVLJZVIVPVKVRWOVHVOUTW
    OVGVNBVLGWOVFVMVFVMIBVLVEGUDZWPOWOUJOUAWOVCVQVJBVLDUBUEQCVSJZVPWCVRWEWQUTWB
    VOWQUSWADGWQURVTURVTICVSDGNZWROPUFWQVJWDVQCVSVEUGUCQDWFJZWCWJWEWLWSWBWIVOWS
    WAWHDWFGWSVTWGVTWGIDWFVSGUDZWTOWSUJOUFWSVQWKWDDWFVLUGUEQVEVLVSWFAMERTBMERTC
    MERTDMERTUHUI $.
    $( [12-May-2012] $) $( [1-May-2012] $)

  ${
    $d x y z f g v w F $.  $d x y z f g v w A $.  $d x z X $.
    $( Value of the recursive definition generator at a class argument.
       (Contributed by Scott Fenton, 26-Mar-2014.) $)
    rdgvalc $p |- ( X e. On -> ( rec ( F , A ) ` X ) = ( ( g e. _V |->
  if ( g = (/) , A , if ( Lim dom g , U. ran g , ( F ` ( g ` U. dom g ) ) ) ) )
                   ` ( rec ( F , A ) |` X ) ) ) $=
      ( vx cv crdg cfv cres cvv c0 wceq cdm wlim crn cuni cif cmpt con0 fveq2
      reseq2 fveq2d eqeq12d rdgval vtoclga ) EFZCAGZHZUGUFIZBJBFZKLAUJMZNUJOPUK
      PUJHCHQQRZHZLDUGHZUGDIZULHZLEDSUFDLZUHUNUMUPUFDUGTUQUIUOULUFDUGUAUBUCEABC
      UDUE $.
      $( [19-Apr-2014] $) $( [26-Mar-2014] $)
  $}


  ${
    $d F g $.  $d I g $.
    $( The value of the recursive definition generator at ` (/) ` when the base
       value is a proper class.  (Contributed by Scott Fenton, 26-Mar-2014.) $)
    rdgprc0 $p |- ( -. I e. _V -> ( rec ( F , I ) ` (/) ) = (/) ) $=
      ( vg cvv wcel wn c0 crdg cfv cv wceq cdm wlim crn cuni cif ax-mp syl eqid
      unieqd cmpt cres con0 0elon rdgvalc res0 fveq2i eqtri wa eqeq1 dmeq limeq
      wb id fveq12d fveq2d ifbieq12d ifbieq2d eleq1d dmmpt elrab2 iftrue eleq1i
      rneq biimpi adantl sylbi con3i ndmfv syl5eq ) BDEZFZGABHZIZGCDCJZGKZBVOLZ
      MZVONZOZVQOZVOIZAIZPZPZUAZIZGVNVMGUBZWFIZWGGUCEVNWIKUDBCAGUEQWHGWFVMUFUGU
      HVLGWFLZEZFWGGKWKVKWKGDEZGGKZBGLZMZGNZOZWNOZGIZAIZPZPZDEZUIVKWEDEXCCGDWJV
      PWEXBDVPVPWMWDXABVOGGUJVPVRWOVTWCWQWTVPVQWNKVRWOUMVOGUKZVQWNULRVPVSWPVOGV
      DTVPWBWSAVPWAWRVOGVPUNVPVQWNXDTUOUPUQURUSCDWEWFWFSUTVAXCVKWLXCVKXBBDWMXBB
      KGSWMBXAVBQVCVEVFVGVHGWFVIRVJ $.
      $( [19-Apr-2014] $) $( [26-Mar-2014] $)
  $}

  ${
    $d F x y z $.  $d I x y z $.
    $( The value of the recursive definition generator when ` I ` is a proper
       class.  (Contributed by Scott Fenton, 26-Mar-2014.) $)
    rdgprc $p |- ( -. I e. _V -> rec ( F , I ) = rec ( F , (/) ) ) $=
      ( vx vz vy cvv wcel cv crdg cfv c0 wceq con0 wral wi fveq2 eqeq12d imbi2d
      weq wb csuc rdgprc0 0ex rdg0 syl6eqr rdgsuc syl5ibr imim2d wlim cres cima
      wn r19.21v cuni crn rneq df-ima 3eqtr4g unieqd a1i wss limord ordsson syl
      word wfn rdgfnon fvreseq mpanl12 bicomd vex wa rdglim mpan 3imtr4d syl5bi
      tfinds com12 ralrimiv eqfnfv2 mp2an sylibr ) BFGULZCHZABIZJZWDAKIZJZLZCMN
      ZWEWGLZWCWICMWDMGWCWIWCDHZWEJZWLWGJZLZOZWCKWEJZKWGJZLZOWCEHZWEJZWTWGJZLZO
      ZWCWTUAZWEJZXEWGJZLZOWCWIODEWDWLKLZWOWSWCXIWMWQWNWRWLKWEPWLKWGPQRDESZWOXC
      WCXJWMXAWNXBWLWTWEPWLWTWGPQRWLXELZWOXHWCXKWMXFWNXGWLXEWEPWLXEWGPQRDCSZWOW
      IWCXLWMWFWNWHWLWDWEPWLWDWGPQRWCWQKWRABUBKAUCUDUEWTMGZXCXHWCXCXHXMXAAJZXBA
      JZLXAXBAPXMXFXNXGXOBWTAUFKWTAUFQUGUHXDEWLNWCXCEWLNZOWLUIZWPWCXCEWLUMXQXPW
      OWCXQWEWLUJZWGWLUJZLZWEWLUKZUNZWGWLUKZUNZLZXPWOXTYEOXQXTYAYCXTXRUOXSUOYAY
      CXRXSUPWEWLUQWGWLUQURUSUTXQXTXPXQWLMVAZXTXPTZXQWLVEYFWLVBWLVCVDWEMVFZWGMV
      FZYFYGBAVGZKAVGZEMWLWEWGVHVIVDVJWLFGZXQWOYETDVKYLXQVLWMYBWNYDBWLFAVMKWLFA
      VMQVNVOUHVPVQVRVSYHYIWKWJTYJYKCMWEWGVTWAWB $.
      $( [19-Apr-2014] $) $( [26-Mar-2014] $)
  $}

  ${
    $d F f $.  $d f g $.  $d F g $.  $d f i $.  $d F i $.  $d f x $.  $d F x $.
    $d f y $.  $d F y $.  $d g i $.  $d g x $.  $d g y $.  $d I f $.  $d I i $.
    $d i x $.  $d I x $.  $d i y $.  $d I y $.  $d x y $.  $d f z $.  $d F z $.
    $d i z $.  $d y z $.
    $( Alternate definition of the recursive function generator when ` I ` is a
       set.  (Contributed by Scott Fenton, 26-Mar-2014.) $)
    dfrdg2 $p |- ( I e. V -> rec ( F , I ) =
       U. { f | E. x e. On ( f Fn x /\
            A. y e. x ( f ` y ) =
              if ( y = (/) , I ,
                   if ( Lim y , U. ( f " y ) ,
                        ( F ` ( f ` U. y ) ) ) ) ) } ) $=
      ( cv cfv c0 wceq cuni cif con0 cvv wcel ax-mp syl fveq2d ifbieq2d iffalse
      vi vg vz crdg wfn wlim cima wral wa wrex cab rdgeq2 eqeq2d ralbidv anbi2d
      ifeq1 rexbidv abbidv unieqd eqeq12d cres cdm crn cmpt df-rdg wel wb resex
      w3a vex eqeq1 wrel relres reldm0 syl6bb dmeq limeq rneq df-ima syl6eqr id
      fveq12d ifbieq12d eqid imaexg uniex fvex ifex fvmpt cin dmres wss 3adant2
      onelss imp fndm 3ad2ant2 sseqtr4d df-ss sylib syl5eq unieq word eloni w3o
      onelon csuc ordzsl iftrue eqtr4d sucid fvres ordunisuc 3eqtr4a wn nsuceq0
      df-ne mpbi nlimsucg mp2b eqtri 3eqtr4g reseq2 syl5ibrcom rexlimiv simp2bi
      df-lim eqtrd 3eqtr4rd 3jaoi sylbi sylan9eqr mpdan 3expa ralbidva pm5.32da
      wne rexbiia abbii unieqi vtoclg ) DUAGZUDZCGZAGZUEZBGZUUDHZUUGIJZUUBUUGUF
      ZUUDUUGUGZKZUUGKZUUDHZDHZLZLZJZBUUEUHZUIZAMUJZCUKZKZJDEUDZUUFUUHUUIEUUPLZ
      JZBUUEUHZUIZAMUJZCUKZKZJUAEFUUBEJZUUCUVDUVCUVKUUBEDULUVLUVBUVJUVLUVAUVICU
      VLUUTUVHAMUVLUUSUVGUUFUVLUURUVFBUUEUVLUUQUVEUUHUUIUUBEUUPUPUMUNUOUQURUSUT
      UUCUUFUUHUUDUUGVAZUBNUBGZIJZUUBUVNVBZUFZUVNVCZKZUVPKZUVNHZDHZLZLZVDZHZJZB
      UUEUHZUIZAMUJZCUKZKUVCABCUBDUUBVEUWKUVBUWJUVACUWIUUTAMUUEMOZUUFUWHUUSUWLU
      UFUIUWGUURBUUEUWLUUFBAVFZUWGUURVGUWLUUFUWMVIZUWFUUQUUHUWNUWFUVMVBZIJZUUBU
      WOUFZUULUWOKZUVMHZDHZLZLZUUQUVMNOUWFUXBJUUDUUGCVJZVHUBUVMUWDUXBNUWEUVNUVM
      JZUVOUWPUWCUXAUUBUXDUVOUVMIJZUWPUVNUVMIVKUVMVLUXEUWPVGUUDUUGVMUVMVNPVOUXD
      UVQUWQUVSUWBUULUWTUXDUVPUWOJUVQUWQVGUVNUVMVPZUVPUWOVQQUXDUVRUUKUXDUVRUVMV
      CUUKUVNUVMVRUUDUUGVSVTUSUXDUWAUWSDUXDUVTUWRUVNUVMUXDWAUXDUVPUWOUXFUSWBRWC
      SUWEWDUWPUUBUXAUAVJUWQUULUWTUUKUUDNOUUKNOUXCUUDUUGNWEPWFUWSDWGWHWHWIPUWNU
      WOUUGJZUXBUUQJUWNUWOUUGUUDVBZWJZUUGUUDUUGWKUWNUUGUXHWLUXIUUGJUWNUUGUUEUXH
      UWLUWMUUGUUEWLZUUFUWLUWMUXJUUEUUGWNWOWMUUFUWLUXHUUEJUWMUUEUUDWPWQWRUUGUXH
      WSWTXAUXGUWNUXBUUIUUBUUJUULUUMUVMHZDHZLZLZUUQUXGUWPUUIUXAUXMUUBUWOUUGIVKU
      XGUWQUUJUWTUXLUULUWOUUGVQUXGUWSUXKDUXGUWRUUMUVMUWOUUGXBRRSSUWNUUGXCZUXNUU
      QJZUWLUWMUXOUUFUWLUWMUIUUGMOUXOUUEUUGXFUUGXDQWMUXOUUIUUGUCGZXGZJZUCMUJZUU
      JXEUXPUCUUGXHUUIUXPUXTUUJUUIUXNUUBUUQUUIUUBUXMXIUUIUUBUUPXIXJUXSUXPUCMUXQ
      MOZUXPUXSUXRIJZUUBUXRUFZUULUXRKZUUDUXRVAZHZDHZLZLZUYBUUBUYCUULUYDUUDHZDHZ
      LZLZJUYAUYGUYKUYIUYMUYAUYFUYJDUYAUXQUYEHZUXQUUDHZUYFUYJUXQUXROUYNUYOJUXQU
      CVJZXKUXQUXRUUDXLPUYAUYDUXQUYEUYAUXQXCUYDUXQJUXQXDUXQXMQZRUYAUYDUXQUUDUYQ
      RXNRUYIUYHUYGUYBXOZUYIUYHJUXRIYQUYRUXQXPUXRIXQXRZUYBUUBUYHTPUXQNOZUYCXOZU
      YHUYGJUYPUXQNXSZUYCUULUYGTXTYAUYMUYLUYKUYRUYMUYLJUYSUYBUUBUYLTPUYTVUAUYLU
      YKJUYPVUBUYCUULUYKTXTYAYBUXSUXNUYIUUQUYMUXSUUIUYBUXMUYHUUBUUGUXRIVKZUXSUU
      JUYCUXLUYGUULUUGUXRVQZUXSUXKUYFDUXSUUMUYDUVMUYEUUGUXRUUDYCUUGUXRXBZWBRSSU
      XSUUIUYBUUPUYLUUBVUCUXSUUJUYCUUOUYKUULVUDUXSUUNUYJDUXSUUMUYDUUDVUERRSSUTY
      DYEUUJUUPUULUUQUXNUUJUULUUOXIUUJUUIXOZUUQUUPJUUJUUGIYQZVUFUUJUXOVUGUUGUUM
      JUUGYGYFUUGIXQWTZUUIUUBUUPTQUUJUXNUXMUULUUJVUFUXNUXMJVUHUUIUUBUXMTQUUJUUL
      UXLXIYHYIYJYKQYLYMXAUMYNYOYPYRYSYTYAUUA $.
      $( [19-Apr-2014] $) $( [26-Mar-2014] $)
  $}

  ${
    $d F f $.  $d f x $.  $d F x $.  $d f y $.  $d F y $.  $d I f $.  $d I x $.
    $d I y $.  $d x y $.
    $( Generalization of ~ dfrdg2 to remove sethood requirement.  (Contributed
       by Scott Fenton, 27-Mar-2014.) $)
    dfrdg3 $p |- rec ( F , I ) =
       U. { f | E. x e. On ( f Fn x /\
            A. y e. x ( f ` y ) =
              if ( y = (/) , if ( I e. _V , I , (/) ) ,
                   if ( Lim y , U. ( f " y ) ,
                        ( F ` ( f ` U. y ) ) ) ) ) } $=
      ( cvv wcel crdg cv cfv c0 wceq cif cuni wral wa con0 wrex cab dfrdg2 wlim
      wfn cima iftrue ifeq1d eqeq2d ralbidv anbi2d rexbidv abbidv unieqd eqtr4d
      wn 0ex ax-mp rdgprc iffalse 3eqtr4a pm2.61i ) EFGZDEHZCIZAIZUBZBIZVBJZVEK
      LZUTEKMZVEUAVBVEUCNVENVBJDJMZMZLZBVCOZPZAQRZCSZNZLUTVAVDVFVGEVIMZLZBVCOZP
      ZAQRZCSZNVPABCDEFTUTVOWBUTVNWACUTVMVTAQUTVLVSVDUTVKVRBVCUTVJVQVFUTVGVHEVI
      UTEKUDUEUFUGUHUIUJUKULUTUMZDKHZVDVFVGKVIMZLZBVCOZPZAQRZCSZNZVAVPKFGWDWKLU
      NABCDKFTUODEUPWCVOWJWCVNWICWCVMWHAQWCVLWGVDWCVKWFBVCWCVJWEVFWCVGVHKVIUTEK
      UQUEUFUGUHUIUJUKURUS $.
      $( [19-Apr-2014] $) $( [27-Mar-2014] $)
  $}

