$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Quantifier-free definitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (x) Bigcup SSet Trans Limits Fix Funs Singletons $.
  $( Declare the syntax for tail cross product. $)
  ctxp $a class ( A (x) B ) $.
  $( Declare the subset relationship class. $)
  csset $a class SSet $.
  $( Declare the transitive set class. $)
  ctrans $a class Trans $.
  $( Declare the set union relationship. $)
  cbigcup $a class Bigcup $.
  $( Declare the class of limit ordinals. $)
  climits $a class Limits $.
  $( Declare the syntax for the fixpoints of a class. $)
  cfix $a class Fix A $.
  $( Declare the syntax for the class of all function. $)
  cfuns $a class Funs $.
  $( Declare the syntax for the class of all singletons. $)
  csingles $a class Singletons $.

  $( Define the tail cross of two classes.  Membership in this class is defined
     by ~ txpss3v and ~ brtxp . $)
  df-txp $a |- ( A (x) B ) = ( ( `' ( 1st |` ( _V X. _V ) ) o. A ) i^i
                               ( `' ( 2nd |` ( _V X. _V ) ) o. B ) ) $.

  $( Define the subset class.  For the value, see ~ brsset . $)
  df-sset $a |- SSet = ( ( _V X. _V ) \ ran ( _E (x) ( _V \ _E ) ) ) $.

  $( Define the class of all transitive sets. $)
  df-trans $a |- Trans = ( _V \ ran ( ( _E o. _E ) \ _E ) ) $.

  $( Define the Bigcup function, which, per ~ fvbigcup , carries a set to its
     union. $)
  df-bigcup $a |- Bigcup = ( ( _V X. _V ) \
     ran ( ( ( _E o. _E ) (x) _V ) (+) ( _V (x) _E ) ) ) $.

  $( Define the class of all fixpoints of a relationship. $)
  df-fix $a |- Fix A = dom ( A i^i _I ) $.

  $( Define the class of all limit ordinals. $)
  df-limits $a |- Limits = ( ( On i^i Fix Bigcup ) \ { (/) } ) $.

  $( Define the class of all functions.  See ~ elfuns for membership. $)
  df-funs $a |- Funs =
    ( ~P ( _V X. _V ) \
      Fix ( _E o. ( ( 1st (x) ( ( _V \ _I ) o. 2nd ) ) o. `' _E ) ) ) $.

  $( Define the class of all singletons.  See ~ elsingles for membership. $)
  df-singles $a |- Singletons =
    dom ( _V \ ran ( ( _E (x) _V ) (+) ( _V (x) _I ) ) ) $.

  $( The binary relationship over ` _V ` always holds.  (Contributed by Scott
     Fenton, 11-Apr-2012.) $)
  brv $p |- A _V B $=
    ( cvv wbr cop wcel opex df-br mpbir ) ABCDABECFABGABCHI $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    $( A tail cross product is a subset of the class of ordered triples.
       (Contributed by Scott Fenton, 31-Mar-2012.) $)
    txpss3v $p |- ( A (x) B ) C_ ( _V X. ( _V X. _V ) ) $=
      ( vx vy vz ctxp c1st cvv cxp cres ccnv ccom c2nd cin df-txp inss1 cv wcel
      wbr visset relco wex cop brcnv brres simprbi adantl exlimiv opelco opelxp
      wa sylbi mpbiran 3imtr4i relssi sstri eqsstri ) ABFGHHIZJZKZALZMURJKBLZNZ
      HURIZABOVCVAVDVAVBPCDVAVDUTAUACQZEQZASZVFDQZUTSZUKZEUBVHURRZVEVHUCZVARVLV
      DRZVJVKEVIVKVGVIVHVFUSSZVKVFVHUSETZDTZUDVNVHVFGSVKVHVFGURVOUEUFULUGUHEVEV
      HUTACTZVPUIVMVEHRVKVEVHHURVPUJVQUMUNUOUPUQ $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  $( A tail cross product is a relationship.  (Contributed by Scott Fenton,
     31-Mar-2012.) $)
  txprel $p |- Rel ( A (x) B ) $=
    ( ctxp wrel cvv cxp wss txpss3v xpss sstri df-rel mpbir ) ABCZDMEEFZGMENFNA
    BHENIJMKL $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A a x y z $.  $d B a x y z $.  $d X a x y z $.  $d Y a x y z $.
    $d Z a x y z $.
    brtxp.1 $e |- X e. _V $.
    brtxp.2 $e |- Y e. _V $.
    brtxp.3 $e |- Z e. _V $.
    $( Characterize a trinary relationship over a tail cross product.  Together
       with ~ txpss3v , this completely defines membership in a tail cross.
       (Contributed by Scott Fenton, 31-Mar-2012.) $)
    brtxp $p |- ( X ( A (x) B ) <. Y , Z >. <-> ( X A Y /\ X B Z ) ) $=
      ( va vz vx vy wbr cvv wa cv wex wceq wcel 3bitri ctxp c1st cres ccnv ccom
      cop cxp c2nd cin df-txp breqi brin opex brco visset brcnv df-br wb copab2
      weq w3a df1st2 eleq2i eqeq2 biidd eqeq1 eloprabg mp3an anbi2i exbii breq2
      syl5bbr biimpac exlimiv eqid anbi12d cla4ev mpan2 impbii df2nd2 anbi12i )
      CDEUFZABUAZMCWBUBNNUGZUCZUDZAUEZUHWDUCZUDZBUEZUIZMCWBWGMZCWBWJMZOCDAMZCEB
      MZOCWBWCWKABUJUKCWBWGWJULWLWNWMWOWLCIPZAMZWPWBWFMZOZIQWQWPDRZOZIQZWNICWBW
      FAFDEUMZUNWSXAIWRWTWQWRWBWPWEMWBWPUFZWESZWTWPWBWEIUOZXCUPWBWPWEUQDNSZENSZ
      WPNSZXEWTURGHXFXEXDJKUTZKLJUSZSXGXHXIVAZWTXKWEXDKLJVBVCXJJPZDRZXNWTKLJDEW
      PNNNKPZDXMVDLPZERXNVEXMWPDVFVGVLVHTVIVJXBWNXAWNIWTWQWNWPDCAVKZVMVNWNDDRZX
      BDVOXAWNXROIDGWTWQWNWTXRXQWPDDVFVPVQVRVSTWMCWPBMZWPWBWIMZOZIQXSWPERZOZIQZ
      WOICWBWIBFXCUNYAYCIXTYBXSXTWBWPWHMXDWHSZYBWPWBWHXFXCUPWBWPWHUQXGXHXIYEYBU
      RGHXFYEXDJLUTZKLJUSZSXLYBYGWHXDKLJVTVCYFYFXMERYBKLJDEWPNNNXODRYFVEXPEXMVD
      XMWPEVFVGVLVHTVIVJYDWOYCWOIYBXSWOWPECBVKZVMVNWOEERZYDEVOYCWOYIOIEHYBXSWOY
      BYIYHWPEEVFVPVQVRVSTWAT $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}


  $( The subset class is a relationship.  (Contributed by Scott Fenton,
     31-Mar-2012.) $)
  relsset $p |- Rel SSet $=
    ( csset wrel cvv cxp wss cep cdif ctxp df-sset difss eqsstri df-rel mpbir
    crn ) ABACCDZEAOFCFGHNZGOIOPJKALM $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A x y $.  $d B x y $.
    brsset.1 $e |- B e. _V $.
    $( For sets, the ` SSet ` binary relationship is equivalent to the subset
       relationship.  (Contributed by Scott Fenton, 31-Mar-2012.) $)
    brsset $p |- ( A SSet B <-> A C_ B ) $=
      ( vx vy csset wbr cvv wcel wss cv wb cep cdif wn wex visset mpbiran bitri
      wa relsset brrelexi ssex wceq breq1 sseq1 bibi12d cop ctxp crn wel wi wal
      opex elrn brtxp brdif brv epelcNEW notbii anbi12i exanali 3bitrri con1bii
      exbii cxp df-br df-sset eleq2i eldif opelxp mpbir2an 3bitri dfss2 3bitr4i
      epel vtoclg pm5.21nii ) ABFGZAHIABJZABFUAUBABCUCDKZBFGZWABJZLVSVTLDAHWAAU
      DWBVSWCVTWAABFUEWAABUFUGWABUHZMHMNZUIZUJZIZOZEDUKZEKZBIZULEUMZWBWCWMWHWHW
      KWDWFGZEPWJWLOZTZEPWMOEWDWFWABUNUOWNWPEWNWKWAMGZWKBWEGZTWPMWEWKWABEQDQZCU
      PWQWJWRWOEDVPWRWKBMGZOZWOWRWKBHGXAWKBHMUQWKBURRWTWLWKBCUSUTSVASVEWJWLEVBV
      CVDWBWDFIWDHHVFZWGNZIZWIWABFVGFXCWDVHVIXDWDXBIZWIWDXBWGVJXEWAHIBHIWABHHCV
      KWSCVLRVMEWABVNVOVQVR $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y z $.
    $( ` _I ` is equal to ` SSet ` and its converse.  (Contributed by Scott
       Fenton, 31-Mar-2012.) $)
    idsset $p |- _I = ( SSet i^i `' SSet ) $=
      ( vy vz cid csset ccnv cin reli wrel relsset relin1 ax-mp weq cv wss eqss
      wa wbr visset brsset bitri ideq brin brcnv anbi12i 3bitr4i eqbrriv ) ABCD
      DEZFZGDHUHHIDUGJKABLAMZBMZNZUJUINZPZUIUJCQUIUJUHQZUIUJOUIUJBRZUAUNUIUJDQZ
      UIUJUGQZPUMUIUJDUGUBUPUKUQULUIUJUOSUQUJUIDQULUIUJDARZUOUCUJUIURSTUDTUEUF
      $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    eltrans.1 $e |- A e. _V $.
    $( Membership in the class of all transitive sets.  (Contributed by Scott
       Fenton, 31-Mar-2012.) $)
    eltrans $p |- ( A e. Trans <-> Tr A ) $=
      ( ctrans wcel cvv cep ccom cdif crn wtr df-trans eleq2i dftr6 bitr4i ) AC
      DAEFFGFHIHZDAJCOAKLABMN $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y $.

    $( A quantifier-free definition of ` On ` .  (Contributed by Scott Fenton,
       5-Apr-2012.) $)
    dfon3 $p |- On =
     ( _V \ ran ( ( SSet i^i ( Trans X. _V ) ) \ ( _I u. _E ) ) ) $=
      ( vy vx con0 cv wa cvv csset ctrans cid cep cdif wcel wn wbr visset bitri
      wex anbi12i wo anbi1i wpss wtr wel wi wal cab cxp cin cun crn dfon2 abeq1
      wceq wb elrn wss brin brsset brxp mpbiran2 eltrans brun ideq epel orbi12i
      notbii ioran brdif dfpss2 an32 anass 3bitr4i exbii exanali 3bitri con2bii
      weq eldif mpbiran bitr4i mpgbir eqtri ) CADZBDZUAZWCUBZEZABUCZUDAUEZBUFZF
      GHFUGZUHZIJUIZKZUJZKZBAUKWJWPUMWIWDWPLZUNBWIBWPULWIWDWOLZMZWQWRWIWRWCWDWN
      NZAQWGWHMZEZAQWIMAWDWNBOZUOWTXBAWCWDWLNZWCWDWMNZMZEWCWDUPZWFEZABVQZMZXAEZ
      EZWTXBXDXHXFXKXDWCWDGNZWCWDWKNZEXHWCWDGWKUQXMXGXNWFWCWDXCURXNWCHLZWFXNXOW
      DFLZWCWDHFXCUSXCUTWCAOVAPRPXFXIWHSZMXKXEXQXEWCWDINZWCWDJNZSXQWCWDIJVBXRXI
      XSWHWCWDXCVCABVDVEPVFXIWHVGPRWCWDWLWMVHXBXHXJEZXAEXLWGXTXAWGXGXJEZWFEXTWE
      YAWFWCWDVITXGXJWFVJPTXHXJXAVKPVLVMWGWHAVNVOVPWQXPWSWDFWOVRXCVSVTWAWB $.
      $( [11-Apr-2012] $) $( [5-Apr-2012] $)
  $}

  $( The ` Bigcup ` relationship is a relationship.  (Contributed by Scott
     Fenton, 11-Apr-2012.) $)
  relbigcup $p |- Rel Bigcup $=
    ( cbigcup wrel cvv cxp cep ccom ctxp csymdif crn cdif relxp ax-mp df-bigcup
    reldif releqi mpbir ) ABCCDZEEFCGCEGHIZJZBZQBTCCKQRNLASMOP $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    brbigcup.1 $e |- B e. _V $.
    $( Binary relationship over ` Bigcup ` .  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    brbigcup $p |- ( A Bigcup B <-> U. A = B ) $=
      ( vx vy vz cbigcup wbr cvv wcel cuni wceq cv cep ctxp wn wb wex wel bitri
      relbigcup brrelexi eleq1 mpbiri uniexb sylibr breq1 unieq eqeq1d cop ccom
      cxp csymdif crn cdif df-br df-bigcup eleq2i wal opex elrn brsymdif visset
      brco epel anbi12i exbii brtxp brv mpbiran2 eluni 3bitr4i mpbiran epelcNEW
      bibi12i notbii exnal 3bitrri con1bii eldif opelxp mpbir2an dfcleq vtoclbg
      wa 3bitri pm5.21nii ) ABGHZAIJZAKZBLZABGUAUBWKWJIJZWIWKWLBIJZCWJBIUCUDAUE
      UFDMZBGHZWNKZBLZWHWKDAIWNABGUGWNALWPWJBWNAUHUIWOWNBUJZGJWRIIULZNNUKZIOZIN
      OZUMZUNZUOZJZWQWNBGUPGXEWRUQURWRXDJZPZEMZWPJZXIBJZQZEUSZXFWQXMXGXGXIWRXCH
      ZERXLPZERXMPEWRXCWNBUTVAXNXOEXNXIWRXAHZXIWRXBHZQZPXOXIWRXAXBVBXRXLXPXJXQX
      KXIWNWTHZEFSZFDSZWEZFRZXPXJXSXIFMZNHZYDWNNHZWEZFRYCFXIWNNNEVCZDVCZVDYGYBF
      YEXTYFYAEFVEFDVEVFVGTXPXSXIBIHWTIXIWNBYHYICVHXIBVIVJFXIWNVKVLXQXIBNHZXKXQ
      XIWNIHYJINXIWNBYHYICVHXIWNVIVMXIBCVNTVOVPTVGXLEVQVRVSXFWRWSJZXHWRWSXDVTYK
      WNIJWMWNBIICWAYICWBVMEWPBWCVLWFWDWG $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d x y z t $.
    $( ` Bigcup ` using maps-to notation.  (Contributed by Scott Fenton,
       16-Apr-2012.) $)
    dfbigcup2 $p |- Bigcup = ( x e. _V |-> U. x ) $=
      ( vy vz vt cbigcup cvv cuni cmpt relbigcup mptrel wceq wbr eqcom brbigcup
      cv visset wcel wa weq eleq1 unieq eqeq2d anbi12d biantrur syl6bbr 3bitr4i
      eqeq1 df-mpt brab eqbrriv ) BCEAFAOZGZHZIAFULJBOZGZCOZKUPUOKZUNUPELUNUPUM
      LUOUPMUNUPCPZNUKFQZDOZULKZRZUTUOKZUQADUNUPUMBPZURABSZVBUNFQZVCRVCVEUSVFVA
      VCUKUNFTVEULUOUTUKUNUAUBUCVFVCVDUDUEUTUPUOUGADFULUHUIUFUJ $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` maps the universe onto itself.  (Contributed by Scott Fenton,
       16-Apr-2012.) $)
    fobigcup $p |- Bigcup : _V -onto-> _V $=
      ( vx vy cvv cbigcup wfo wfn wceq df-fo cv cuni wcel wral uniexg dfbigcup2
      crn rgen mptfng mpbi wrex cab rnmpt visset snex unisn eqcomi unieq eqeq2d
      csn rcla4ev mp2an 2th abbi2i eqtr4i mpbir2an ) CCDEDCFZDOZCGCCDHAIZJZCKZA
      CLUOUSACUQCMPACURDANZQRUPBIZURGZACSZBTCABCURDUTUAVCBCVACKVCBUBZVAUHZCKVAV
      EJZGZVCVAUCVFVAVAVDUDUEVBVGAVECUQVEGURVFVAUQVEUFUGUIUJUKULUMUN $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` is a function over the universal class.  (Contributed by
       Scott Fenton, 11-Apr-2012.) $)
    fnbigcup $p |- Bigcup Fn _V $=
      ( cvv cbigcup wfo wfn fobigcup fofn ax-mp ) AABCBADEAABFG $.
      $( [11-Apr-2012] $)
  $}

  ${
    fvbigcup.1 $e |- A e. _V $.
    $( For sets, ` Bigcup ` yields union.  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    fvbigcup $p |- ( Bigcup ` A ) = U. A $=
      ( cbigcup cfv cuni wceq wbr eqid uniex brbigcup mpbir cvv wfn wb fnbigcup
      wcel fnbrfvb mp2an ) ACDAEZFZASCGZUASSFSHASABIZJKCLMALPTUANOBLASCUBQRK $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix.1 $e |- A e. _V $.
    $( Membership in the fixpoints of a class.  (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    elfix $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cid cin cdm cv wbr wex df-fix eleq2i eldm wceq bitri breq2
      wa brin visset ideq anbi2i exbii biimparc exlimiv eqid eqeq2 cla4ev mpan2
      anbi12d impbii 3bitri ) ABEZFABGHZIZFADJZUOKZDLZAABKZUNUPABMNDAUOCOUSAUQB
      KZAUQPZSZDLZUTURVCDURVAAUQGKZSVCAUQBGTVEVBVAAUQDUAUBUCQUDVDUTVCUTDVBUTVAA
      UQABRUEUFUTAAPZVDAUGVCUTVFSDACUQAPVAUTVBVFUQAABRUQAAUHUKUIUJULQUM $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix2.1 $e |- Rel R $.
    $( Alternative membership in the fixpoint of a class.  (Contributed by
       Scott Fenton, 11-Apr-2012.) $)
    elfix2 $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cvv wbr elisset brrelexi cv wb eleq1 breq12 anidms bibi12d
      wceq visset elfix vtoclg pm5.21nii ) ABEZFZAGFAABHZAUBIAABCJDKZUBFZUEUEBH
      ZLUCUDLDAGUEAQZUFUCUGUDUEAUBMUHUGUDLUEAUEABNOPUEBDRSTUA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.
    $( The fixpoints of a class in terms of its range.  (Contributed by Scott
       Fenton, 16-Apr-2012.) $)
    dffix2 $p |- Fix A = ran ( A i^i _I ) $=
      ( vx vy cfix cid cin crn cv wbr weq wex wcel equid visset equequ1 anbi12d
      wa breq1 cla4ev bitri mpan2 biimpac exlimiv impbii elfix elrn brin anbi2i
      ideq exbii 3bitr4i eqriv ) BADZAEFZGZBHZUPAIZCHZUPAIZCBJZQZCKZUPUMLUPUOLZ
      UQVBUQBBJZVBBMVAUQVDQCUPBNZUTUSUQUTVDURUPUPARZCBBOPSUAVAUQCUTUSUQVFUBUCUD
      UPAVEUEVCURUPUNIZCKVBCUPUNVEUFVGVACVGUSURUPEIZQVAURUPAEUGVHUTUSURUPVEUIUH
      TUJTUKUL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoints of a class are a subset of its domain.  (Contributed by
     Scott Fenton, 16-Apr-2012.) $)
  fixssdm $p |- Fix A C_ dom A $=
    ( cfix cid cin cdm df-fix wss inss1 dmss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  $( The fixpoints of a class are a subset of its range.  (Contributed by Scott
     Fenton, 16-Apr-2012.) $)
  fixssrn $p |- Fix A C_ ran A $=
    ( cfix cid cin crn dffix2 wss inss1 rnss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    $( The fixpoints of a class are the same as those of its converse.
       (Contributed by Scott Fenton, 16-Apr-2012.) $)
    fixcnv $p |- Fix A = Fix `' A $=
      ( vx cfix ccnv cv wbr wcel visset brcnv elfix 3bitr4ri eqriv ) BACZADZCZB
      EZPNFPPAFPOGPMGPPABHZQIPNQJPAQJKL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoint operator distributes over union.  (Contributed by Scott
     Fenton, 16-Apr-2012.) $)
  fixun $p |- Fix ( A u. B ) = ( Fix A u. Fix B ) $=
    ( cun cid cin cdm cfix indir dmeqi dmun eqtri df-fix uneq12i 3eqtr4i ) ABCZ
    DEZFZADEZFZBDEZFZCZOGAGZBGZCQRTCZFUBPUEABDHIRTJKOLUCSUDUAALBLMN $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    ellimits.1 $e |- A e. _V $.
    $( Membership in the class of all limit ordinals.  (Contributed by Scott
       Fenton, 11-Apr-2012.) $)
    ellimits $p |- ( A e. Limits <-> Lim A ) $=
      ( con0 cbigcup cfix cin wcel c0 csn wn wa word cuni wceq wne climits wlim
      3bitri anbi12i bitri elin elon elfix brbigcup eqcom elsnc necon3bbii cdif
      wbr df-limits eleq2i eldif w3a df-lim df-3an an32 3bitr4i ) ACDEZFZGZAHIZ
      GZJZKZALZAAMZNZKZAHOZKZAPGZAQZUTVHVCVIUTACGZAURGZKVHACURUAVMVEVNVGABUBVNA
      ADUIVFANVGADBUCAABUDVFAUERSTVBAHAHBUFUGSVKAUSVAUHZGVDPVOAUJUKAUSVAULTVLVE
      VIVGUMVEVIKVGKVJAUNVEVIVGUOVEVIVGUPRUQ $.
      $( [11-Apr-2012] $)
  $}

  $( The class of all limit ordinals is a subclass of the class of all
     ordinals.  (Contributed by Scott Fenton, 11-Apr-2012.) $)
  limitssson $p |- Limits C_ On $=
    ( vx climits con0 cv wcel wi dfss2 wlim word limord visset ellimits 3imtr4i
    wss elon mpgbir ) BCNADZBEZQCEZFAABCGQHQIRSQJQAKZLQTOMP $.
    $( [11-Apr-2012] $)

  ${
    $d x y $.
    $( A quantifier-free definition of ` om ` that does not depend on
       ~ ax-inf .  (Note: label was changed from ~ dfom5 to ~ dfom5b to prevent
       naming conflict.  NM 12-Feb-2013) (Contributed by Scott Fenton,
       11-Apr-2012.) $)
    dfom5b $p |- om = ( On i^i |^| Limits ) $=
      ( vx vy cv word cab wlim wel wi wal cin con0 climits cint inab df-on wcel
      wa com df-int visset ellimits imbi1i albii abbii eqtri ineq12i 3eqtr4ri
      df-om ) ACDZAEZBCZFZABGZHZBIZAEZJUIUOQAEKLMZJRUIUOANKUJUQUPAOUQUKLPZUMHZB
      IZAEUPABLSUTUOAUSUNBURULUMUKBTUAUBUCUDUEUFABUHUG $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d F x y z w t a b c d $.
    elfuns.1 $e |- F e. _V $.
    $( Membership in the class of all functions.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    elfuns $p |- ( F e. Funs <-> Fun F ) $=
      ( vw vy vz va vb vd cv c1st cvv c2nd wbr wn wa wcel weq wi wb wex vt wrel
      vx vc cid cdif ccom ctxp wral cop cfuns wfun wceq breq2 notbid breq1 opex
      wal visset brtxp cfv op1st wfn wfo fo1st fofn ax-mp fnbrfvb mp2an mpbiran
      mpbi syl6bb rcla42v brco fo2nd fofun op2nd funbreq brdif brv notbii bitri
      ideq anbi12i exbii exanali 3bitri con2bii equequ2 equequ1 imbi12d a4v mpi
      equid eqcomd sylbir syl6com alrimiv alrimivv elrel ex ee4anv opeq1 eleq1d
      anbi2d opeq12 3adant3 3adant2 anbi12d eqeq12 3adant1 cla43gv mp3an syl6bi
      w3a com12 eqtr syl8 com3l imbi2i imnan bitr2i syl6ibr eleq1 breq12 ancoms
      bi2anan9 imbi2d mpbiri exlimivv a1i syl2and pm2.43d ralrimdvv impbid2 cep
      com23 wrex bitr4i 3bitr4i pm5.32i ccnv cfix eldif elfix coep coepr dfrex2
      cxp cpw rexbii rexnal anbi2i df-funs eleq2i wss df-rel elpw anbi1i dffun4
      ) AUBZUAIZCIZJKUEUFZLUGZUHZMZNZUAAUIZCAUIZOZUVAUCIZDIZUJZAPZUVLEIZUJZAPZO
      ZDEQZRZEURZDURUCURZOAUKPZAULUVAUVJUWCUVAUVJUWCUVJUWBUCDUVJUWAEUVSUVJUVQUV
      MUVEMZNZUVTUVHUWFUVBUVNUVFMZNCUAUVNUVQAAUVCUVNUMUVGUWGUVCUVNUVBUVFUNUOUVB
      UVQUMZUWGUWEUWHUWGUVQUVNUVFMZUWEUVBUVQUVNUVFUPUWIUVQUVLJMZUWEJUVEUVQUVLUV
      MUVLUVPUQZUCUSZDUSZUTUVQJVAUVLUMZUWJUVLUVPUWLVBJKVCZUVQKPZUWNUWJSKKJVDZUW
      OVEKKJVFVGZUWKKUVQUVLJUWLVHVIVKVJVLUOVMUWFECQZCDQZRZCURZUVTUWEUXBUWEUVQUV
      CLMZUVCUVMUVDMZOZCTUWSUWTNZOZCTUXBNCUVQUVMUVDLUWKUWMVNUXEUXGCUXCUWSUXDUXF
      LULZUVQUVPLMZUXCUWSSKKLVDZUXHVOKKLVPVGZUVQLVAUVPUMZUXIUVLUVPUWLEUSZVQLKVC
      ZUWPUXLUXISUXJUXNVOKKLVFVGZUWKKUVQUVPLUXMVHVIVKUVQUVPUVCLUWKUXMCUSZVRVIUX
      DUVCUVMUEMZNZUXFUXDUVCUVMKMUXRUVCUVMKUEVSUVCUVMVTVJUXQUWTUVCUVMUWMWCWAWBW
      DWEUWSUWTCWFWGWHUXBUVPUVMUXBEEQZEDQZEWNUXAUXSUXTRCECEQUWSUXSUWTUXTCEEWICE
      DWJWKWLWMWOWPWQWRWSUVAUWCUVHCUAAAUVAUVCAPZUVBAPZOZUWCUVHUVAUYCUWCUVHRZUVA
      UYAUVCFIZGIZUJZUMZGTFTZUYBUVBUDIZHIZUJZUMZHTUDTZUYCUYDRZUVAUYAUYIFGUVCAWT
      XAUVAUYBUYNUDHUVBAWTXAUYIUYNOZUYORUVAUYPUYHUYMOZHTUDTZGTFTUYOUYHUYMFGUDHX
      BUYRUYOFGUYQUYOUDHUYQUYOUYGAPZUYLAPZOZUWCUDFQZHCQZCGQZNZOZCTZOZNZRZRVUAUW
      CVUBVUCVUDRZCURZRZVUIVUBVUAUWCVULVUBVUAUWCGHQZVULVUBVUAUYSUYEUYKUJZAPZOZU
      WCVUNRVUBUYTVUPUYSVUBUYLVUOAUYJUYEUYKXCXDXEUWCVUQVUNUYEKPUYFKPUYKKPUWCVUQ
      VUNRZRFUSZGUSZHUSZUWAVURUCDEUYEUYFUYKKKKUCFQZDGQZEHQZXOZUVSVUQUVTVUNVVEUV
      OUYSUVRVUPVVBVVCUVOUYSSVVDVVBVVCOUVNUYGAUVLUVMUYEUYFXFXDXGVVBVVDUVRVUPSVV
      CVVBVVDOUVQVUOAUVLUVPUYEUYKXFXDXHXIVVCVVDUVTVUNSVVBUVMUYFUVPUYKXJXKWKXLXM
      XPXNVUNVUKCVUNVUCVUDVUNVUCOUYFUVCUYFUYKUVCXQWOXAWRXRXSVUMVUBVUGNZRVUIVULV
      VFVUBVUGVULVUCVUDCWFWHXTVUBVUGYAYBYCUYQUYCVUAUYDVUJUYHUYAUYSUYMUYBUYTUVCU
      YGAYDUVBUYLAYDYGUYQUVHVUIUWCUYMUYHUVHVUISUYMUYHOZUVGVUHVVGUVGUYLUYGUVFMZV
      UHUVBUYLUVCUYGUVFYEVVHUYLUYEJMZUYLUYFUVEMZOVUHJUVEUYLUYEUYFUYJUYKUQZVUSVU
      TUTVVIVUBVVJVUGJULZUYLUYJJMZVVIVUBSUWQVVLVEKKJVPVGUYLJVAUYJUMZVVMUYJUYKUD
      USZVBUWOUYLKPZVVNVVMSUWRVVKKUYLUYJJVVOVHVIVKUYLUYJUYEJVVKVVOVUSVRVIVVJUYL
      UVCLMZUVCUYFUVDMZOZCTVUGCUYLUYFUVDLVVKVUTVNVVSVUFCVVQVUCVVRVUEUXHUYLUYKLM
      ZVVQVUCSUXKUYLLVAUYKUMZVVTUYJUYKVVOVVAVQUXNVVPVWAVVTSUXOVVKKUYLUYKLVVAVHV
      IVKUYLUYKUVCLVVKVVAUXPVRVIVVRUVCUYFUEMZNZVUEVVRUVCUYFKMVWCUVCUYFKUEVSUVCU
      YFVTVJVWBVUDUVCUYFVUTWCWAWBWDWEWBWDWBVLUOYFYHWKYIYJYJWPYKYLYMYQYNYOUUAAKK
      UUIZUUJZYPUVFYPUUBUGZUGZUUCZUFZPZAVWEPZUVJOZUWDUVKVWJVWKAVWHPZNZOVWLAVWEV
      WHUUDUVJVWNVWKVWMUVJVWMAAVWGMAUVCVWFMZCAYRZUVJNZAVWGBUUECAAVWFBBUUFVWPUVI
      NZCAYRVWQVWOVWRCAVWOUVGUAAYRVWRUAAUVCUVFBUXPUUGUVGUAAUUHWBUUKUVICAUULWBWG
      WHUUMYSUKVWIAUUNUUOUVAVWKUVJUVAAVWDUUPVWKAUUQAVWDBUURYSUUSYTUCDEAUUTYT $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d A x y z $.
    $( Membership in the class of all singletons.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    elsingles $p |- ( A e. Singletons <-> E. x A = { x } ) $=
      ( vy vz csingles wcel cvv cv wceq wex eleq1 cep ctxp cid wbr visset wb wn
      wal brv csn elisset snex mpbiri exlimiv eqeq1 exbidv csymdif crn cdif cdm
      df-singles eleq2i wel cop brtxp mpbiran2 mpbiran bibi12i brsymdif con2bii
      eldm epel weq ideq elsn bitr4i 3bitr3ri albii brdif df-br opex elrn df-ex
      dfcleq 3bitri 3bitr4ri exbii vtoclbg pm5.21nii ) BEFZBGFZBAHZUAZIZAJZBEUB
      WEWBAWEWBWDGFWCUCBWDGKUDUECHZEFZWGWDIZAJZWAWFCBGWGBEKWGBIWIWEAWGBWDUFUGWH
      WGGLGMZGNMZUHZUIZUJZUKZFWGWCWOOZAJWJEWPWGULUMAWGWOCPZVBWQWIADCUNZDHZWDFZQ
      ZDSWTWGWCUOZWMOZRZDSZWIWQXBXEDWTXCWKOZWTXCWLOZQZWTWGLOZWTWCNOZQXEXBXGXJXH
      XKXGXJWTWCGOLGWTWGWCDPZWRAPZUPWTWCTUQXHWTWGGOXKGNWTWGWCXLWRXMUPWTWGTURUSX
      DXIWTXCWKWLUTVAXJWSXKXADCVCXKDAVDXAWTWCXMVEDWCVFVGUSVHVIDWGWDVOWQWGWCWNOZ
      RZXFWQWGWCGOXOWGWCGWNVJWGWCTURXNXFXNXCWNFXDDJXFRWGWCWNVKDXCWMWGWCVLVMXDDV
      NVPVAVGVQVRVPVSVT $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d A x $.
    snelsingles.1 $e |- A e. _V $.
    $( A singleton is a member of the class of all singletons.  (Contributed by
       Scott Fenton, 19-Feb-2013.) $)
    snelsingles $p |- { A } e. Singletons $=
      ( vx csn csingles wcel cv wceq wex cvv isset eqcom exbii bitri mpbi eximi
      sneq ax-mp elsingles mpbir ) ADZEFUACGZDHZCIZAUBHZCIZUDAJFZUFBUGUBAHZCIUF
      CAKUHUECUBALMNOUEUCCAUBQPRCUAST $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d x y z $.  $d ph y z $.
    $( A definiton of iota using minimal quantifiers.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    dfiota3 $p |- ( iota x ph ) = U. U. ( { { x | ph } } i^i Singletons ) $=
      ( vy vz cab cv csn wceq cuni csingles cin wcel wrex snid sneq mpbi mpbiri
      eleq2 wa wi df-iota wb abeq1 snex visset rcla4ev mp2an ineq1d snelsingles
      cio wss snss df-ss syl6eq rexeqdv elin anbi1i bitri wex elsingles equcoms
      elsn syl6bb eqeq1 imbi12d exlimiv sylbi imbi1d imp rexlimiv impbii eluni2
      eleq1 bitr4i mpgbir unieqi eqtri ) ABUJABEZCFZGZHZCEZIVRGZJKZIZIABCUAWBWE
      WBWEHWAVSWELZUBCWACWEUCWAVSDFZLZDWDMZWFWAWIWAWIWHDVTGZMZVTWJLVSVTLZWKVTVS
      UDZNVSCUEZNWHWLDVTWJWGVTVSRUFUGWAWHDWDWJWAWDWJJKZWJWAWCWJJVRVTOUHWJJUKZWO
      WJHVTJLWPVSWNUIVTJWMULPWJJUMPUNUOQWHWADWDWGWDLZWGVRHZWGJLZSZWHWATZWQWGWCL
      ZWSSWTWGWCJUPXBWRWSDVRVBUQURWRWSXAWRWSXATVRJLZVSVRLZWATZTXCVRWGGZHZDUSXED
      VRUTXGXEDXGXEVSWGHZXFVTHZTXIDCWGVSOVAXGXDXHWAXIXGXDVSXFLXHVRXFVSRCWGVBVCV
      RXFVTVDVEQVFVGWRWSXCXAXEWGVRJVMWRWHXDWAWGVRVSRVHVEQVIVGVJVKDVSWDVLVNVOVPV
      Q $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d F x $.  $d A x $.
    $( Another quantifier free definition of function value.  (Contributed by
       Scott Fenton, 19-Feb-2013.) $)
    dffv4 $p |- ( F ` A ) = U. U. ( { ( F " { A } ) } i^i Singletons ) $=
      ( vx cfv cv csn cima wcel cio cab csingles cuni dffv3 dfiota3 abid2 sneqi
      cin ineq1i unieqi 3eqtri ) ABDCEBAFGZHZCIUBCJZFZKQZLZLUAFZKQZLZLCABMUBCNU
      FUIUEUHUDUGKUCUACUAOPRSST $.
      $( [19-Feb-2013] $)
  $}

