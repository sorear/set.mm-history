$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Quantifier-free definitions
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (x) Bigcup SSet Trans Limits (++) Fix $.
  $( Declare the syntax for symmetric difference. $)
  csymdif $a class ( A (++) B ) $.
  $( Declare the syntax for tail cross product. $)
  ctxp $a class ( A (x) B ) $.
  $( Declare the subset relationship class. $)
  csset $a class SSet $.
  $( Declare the transitive set class. $)
  ctrans $a class Trans $.
  $( Declare the set union relationship. $)
  cbigcup $a class Bigcup $.
  $( Declare the class of limit ordinals. $)
  climits $a class Limits $.
  $( Declare the syntax for the fixpoints of a class. $)
  cfix $a class Fix A $.

  $( Define the symmetric difference of two classes. $)
  df-symdif $a |- ( A (++) B ) = ( ( A \ B ) u. ( B \ A ) ) $.

  $( Define the tail cross of two classes.  Membership in this class is defined
     by ~ txpss3v and ~ brtxp . $)
  df-txp $a |- ( A (x) B ) = ( ( `' ( 1st |` ( _V X. _V ) ) o. A ) i^i
                               ( `' ( 2nd |` ( _V X. _V ) ) o. B ) ) $.

  $( Define the subset class.  For the value, see ~ brsset . $)
  df-sset $a |- SSet = ( ( _V X. _V ) \ ran ( _E (x) ( _V \ _E ) ) ) $.

  $( Define the class of all transitive sets. $)
  df-trans $a |- Trans = ( _V \ ran ( ( _E o. _E ) \ _E ) ) $.

  $( Define the Bigcup function, which, per ~ fvbigcup , carries a set to its
     union. $)
  df-bigcup $a |- Bigcup = ( ( _V X. _V ) \
     ran ( ( ( _E o. _E ) (x) _V ) (++) ( _V (x) _E ) ) ) $.

  $( Define the class of all fixpoints of a relationship. $)
  df-fix $a |- Fix A = dom ( A i^i _I ) $.

  $( Define the class of all limit ordinals. $)
  df-limits $a |- Limits = ( ( On i^i Fix Bigcup ) \ { (/) } ) $.

  $( Symmetric difference commutes. $)
  symdifcom $p |- ( A (++) B ) = ( B (++) A ) $=
    ( cdif cun csymdif uncom df-symdif 3eqtr4i ) ABCZBACZDJIDABEBAEIJFABGBAGH
    $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Equality theorem for symmetric difference. $)
  symdifeq1 $p |- ( A = B -> ( A (++) C ) = ( B (++) C ) ) $=
    ( wceq cdif cun csymdif difeq1 difeq2 uneq12d df-symdif 3eqtr4g ) ABDZACEZC
    AEZFBCEZCBEZFACGBCGMNPOQABCHABCIJACKBCKL $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Equality theorem for symmetric difference. $)
  symdifeq2 $p |- ( A = B -> ( C (++) A ) = ( C (++) B ) ) $=
    ( wceq csymdif symdifeq1 symdifcom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Membership in a symmetric difference. $)
  elsymdif $p |- ( A e. ( B (++) C ) <-> -. ( A e. B <-> A e. C ) ) $=
    ( cdif cun wcel wn wa wo csymdif wb elun eldif orbi12i bitri df-symdif
    eleq2i xor 3bitr4i ) ABCDZCBDZEZFZABFZACFZGHZUEUDGHZIZABCJZFUDUEKGUCATFZAUA
    FZIUHATUALUJUFUKUGABCMACBMNOUIUBABCPQUDUERS $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  $( Symmetric difference with the empty class. $)
  symdif0 $p |- ( A (++) (/) ) = A $=
    ( c0 csymdif cdif cun df-symdif dif0 0dif uneq12i un0 3eqtri ) ABCABDZBADZE
    ABEAABFLAMBAGAHIAJK $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Symmetric difference with the universal class. $)
  symdifV $p |- ( A (++) _V ) = ( _V \ A ) $=
    ( cvv csymdif cdif cun c0 df-symdif wss wceq ssv ssdif0 mpbi uneq1i uncom
    un0 eqtri 3eqtri ) ABCABDZBADZEFSEZSABGRFSABHRFIAJABKLMTSFESFSNSOPQ $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Symmetric difference yields the empty class with the same argument
     twice. $)
  symdifid $p |- ( A (++) A ) = (/) $=
    ( csymdif cdif cun c0 df-symdif difid uneq12i un0 3eqtri ) AABAACZKDEEDEAAF
    KEKEAGZLHEIJ $.
    $( [12-May-2012] $) $( [25-Apr-2012] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Symmetric difference associates. $)
    symdifass $p |- ( A (++) ( B (++) C ) ) = ( ( A (++) B ) (++) C ) $=
      ( vx csymdif cv wcel wb wn biass notbii xor3 notbi bitr4i con1bii
      3bitr3ri elsymdif bibi2i bibi1i 3bitr4i eqriv ) DABCEZEZABEZCEZDFZAGZUFUB
      GZHZIUFUDGZUFCGZHZIUFUCGUFUEGUIULUGUFBGZUKHZIZHZUGUMHZIZUKHZUIULUQUKHZIUG
      UNHZIUSUPUTVAUGUMUKJKUSUTUSIURUKIHUTURUKLUQUKMNOUGUNLPUHUOUGUFBCQRUJURUKU
      FABQSTKUFAUBQUFUDCQTUA $.
      $( [12-May-2012] $) $( [24-Apr-2012] $)
  $}

  $( The binary relationship over ` _V ` always holds. $)
  brv $p |- A _V B $=
    ( cvv wbr cop wcel opex df-br mpbir ) ABCDABECFABGABCHI $.
    $( [11-Apr-2012] $)

  $( The binary relationship of a symmetric difference. $)
  brsymdif $p |- ( A ( R (++) S ) B <-> -. ( A R B <-> A S B ) ) $=
    ( cop csymdif wcel wb wn wbr elsymdif df-br bibi12i notbii 3bitr4i ) ABEZCD
    FZGPCGZPDGZHZIABQJABCJZABDJZHZIPCDKABQLUCTUARUBSABCLABDLMNO $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    $( A tail cross product is a subset of the class of ordered triples. $)
    txpss3v $p |- ( A (x) B ) C_ ( _V X. ( _V X. _V ) ) $=
      ( vx vy vz ctxp c1st cvv cxp cres ccnv ccom c2nd cin df-txp inss1 relco
      cv wbr wa wex wcel cop visset brcnv brres simprbi sylbi adantl 19.23aiv
      opelco opelxp mpbiran 3imtr4i relssi sstri eqsstri ) ABFGHHIZJZKZALZMURJK
      BLZNZHURIZABOVCVAVDVAVBPCDVAVDUTAQCRZERZASZVFDRZUTSZTZEUAVHURUBZVEVHUCZVA
      UBVLVDUBZVJVKEVIVKVGVIVHVFUSSZVKVFVHUSEUDZDUDZUEVNVHVFGSVKVHVFGURVOUFUGUH
      UIUJEVEVHUTACUDZVPUKVMVEHUBVKVEVHHURVPULVQUMUNUOUPUQ $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  $( A tail cross product is a relationship. $)
  txprel $p |- Rel ( A (x) B ) $=
    ( ctxp wrel cvv cxp wss txpss3v xpss sstri df-rel mpbir ) ABCZDMEEFZGMENFNA
    BHENIJMKL $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A a x y z $.  $d B a x y z $.  $d X a x y z $.  $d Y a x y z $.
    $d Z a x y z $.
    brtxp.1 $e |- X e. _V $.
    brtxp.2 $e |- Y e. _V $.
    brtxp.3 $e |- Z e. _V $.
    $( Characterize a trinary relationship over a tail cross product.  Together
       with ~ txpss3v , this completely defines membership in a tail cross. $)
    brtxp $p |- ( X ( A (x) B ) <. Y , Z >. <-> ( X A Y /\ X B Z ) ) $=
      ( va vz vx vy cop ctxp wbr c1st cvv cxp cres ccnv ccom c2nd cin wa
      df-txp breqi brin cv wex wceq opex brco wcel visset brcnv df-br wb w3a
      weq copab2 eqeq2 biidd eqeq1 eloprabg df1st2 eleq2i syl5bbr mp3an 3bitri
      anbi2i exbii breq2 biimpac 19.23aiv eqid anbi12d cla4ev mpan2 impbii
      df2nd2 anbi12i ) CDEMZABNZOCWBPQQRZSZTZAUAZUBWDSZTZBUAZUCZOCWBWGOZCWBWJOZ
      UDCDAOZCEBOZUDCWBWCWKABUEUFCWBWGWJUGWLWNWMWOWLCIUHZAOZWPWBWFOZUDZIUIWQWPD
      UJZUDZIUIZWNICWBWFAFDEUKZULWSXAIWRWTWQWRWBWPWEOWBWPMZWEUMZWTWPWBWEIUNZXCU
      OWBWPWEUPDQUMZEQUMZWPQUMZXEWTUQGHXFXGXHXIURZXDJKUSZKLJUTZUMWTXEXKJUHZDUJZ
      XNWTKLJDEWPQQQKUHZDXMVALUHZEUJXNVBXMWPDVCVDXLWEXDKLJVEVFVGVHVIVJVKXBWNXAW
      NIWTWQWNWPDCAVLZVMVNWNDDUJZXBDVOXAWNXRUDIDGWTWQWNWTXRXQWPDDVCVPVQVRVSVIWM
      CWPBOZWPWBWIOZUDZIUIXSWPEUJZUDZIUIZWOICWBWIBFXCULYAYCIXTYBXSXTWBWPWHOXDWH
      UMZYBWPWBWHXFXCUOWBWPWHUPXGXHXIYEYBUQGHXFXJXDJLUSZKLJUTZUMYBYEYFYFXMEUJYB
      KLJDEWPQQQXODUJYFVBXPEXMVAXMWPEVCVDYGWHXDKLJVTVFVGVHVIVJVKYDWOYCWOIYBXSWO
      WPECBVLZVMVNWOEEUJZYDEVOYCWOYIUDIEHYBXSWOYBYIYHWPEEVCVPVQVRVSVIWAVI $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}


  $( The subset class is a relationship. $)
  relsset $p |- Rel SSet $=
    ( csset wrel cvv cxp wss cep cdif ctxp crn df-sset difss eqsstri df-rel
    mpbir ) ABACCDZEAOFCFGHIZGOJOPKLAMN $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  ${
    $d A x y $.  $d B x y $.
    brsset.1 $e |- B e. _V $.
    $( For sets, the ` SSet ` binary relationship is equivalent to the subset
       relationship. $)
    brsset $p |- ( A SSet B <-> A C_ B ) $=
      ( vx vy csset wbr cvv wcel wss relsset brrelexi ssex cv wb wceq breq1
      sseq1 bibi12d cop cep cdif ctxp crn wn wel wi wal wex wa opex elrn
      visset brtxp epel brdif brv mpbiran epelcNEW notbii bitri anbi12i exbii
      exanali 3bitrri con1bii cxp df-br df-sset eleq2i eldif opelxp mpbir2an
      3bitri dfss2 3bitr4i vtoclg pm5.21nii ) ABFGZAHIABJZABFKLABCMDNZBFGZWABJZ
      OVSVTODAHWAAPWBVSWCVTWAABFQWAABRSWABTZUAHUAUBZUCZUDZIZUEZEDUFZENZBIZUGEUH
      ZWBWCWMWHWHWKWDWFGZEUIWJWLUEZUJZEUIWMUEEWDWFWABUKULWNWPEWNWKWAUAGZWKBWEGZ
      UJWPUAWEWKWABEUMDUMZCUNWQWJWRWOEDUOWRWKBUAGZUEZWOWRWKBHGXAWKBHUAUPWKBUQUR
      WTWLWKBCUSUTVAVBVAVCWJWLEVDVEVFWBWDFIWDHHVGZWGUBZIZWIWABFVHFXCWDVIVJXDWDX
      BIZWIWDXBWGVKXEWAHIBHIWABHHCVLWSCVMURVNEWABVOVPVQVR $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y z $.
    $( ` _I ` is equal to ` SSet ` and its converse. $)
    idsset $p |- _I = ( SSet i^i `' SSet ) $=
      ( vy vz cid csset ccnv cin reli wrel relsset relin1 ax-mp weq cv wss wa
      wbr eqss visset ideq brin brsset brcnv bitri anbi12i 3bitr4i eqbrriv ) AB
      CDDEZFZGDHUHHIDUGJKABLAMZBMZNZUJUINZOZUIUJCPUIUJUHPZUIUJQUIUJBRZSUNUIUJDP
      ZUIUJUGPZOUMUIUJDUGTUPUKUQULUIUJUOUAUQUJUIDPULUIUJDARZUOUBUJUIURUAUCUDUCU
      EUF $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    eltrans.1 $e |- A e. _V $.
    $( Membership in the class of all transitive sets. $)
    eltrans $p |- ( A e. Trans <-> Tr A ) $=
      ( ctrans wcel cvv cep ccom cdif crn wtr df-trans eleq2i dftr6 bitr4i ) AC
      DAEFFGFHIHZDAJCOAKLABMN $.
      $( [11-Apr-2012] $) $( [31-Mar-2012] $)
  $}

  ${
    $d x y $.

    $( A quantifier-free definition of ` On ` . $)
    dfon3 $p |- On =
     ( _V \ ran ( ( SSet i^i ( Trans X. _V ) ) \ ( _I u. _E ) ) ) $=
      ( vy vx con0 cv wpss wtr wa wel wi wal cab cvv csset ctrans cxp cin cid
      cep cun cdif crn dfon2 wceq wcel wb abeq1 wn wbr wex visset elrn wss weq
      brin brsset brxp mpbiran2 eltrans bitri anbi12i wo brun ideq epel
      orbi12i notbii ioran brdif dfpss2 anbi1i an32 anass 3bitr4i exbii
      exanali 3bitri con2bii eldif mpbiran bitr4i mpgbir eqtri ) CADZBDZEZWCFZG
      ZABHZIAJZBKZLMNLOZPZQRSZTZUAZTZBAUBWJWPUCWIWDWPUDZUEBWIBWPUFWIWDWOUDZUGZW
      QWRWIWRWCWDWNUHZAUIWGWHUGZGZAUIWIUGAWDWNBUJZUKWTXBAWCWDWLUHZWCWDWMUHZUGZG
      WCWDULZWFGZABUMZUGZXAGZGZWTXBXDXHXFXKXDWCWDMUHZWCWDWKUHZGXHWCWDMWKUNXMXGX
      NWFWCWDXCUOXNWCNUDZWFXNXOWDLUDZWCWDNLXCUPXCUQWCAUJURUSUTUSXFXIWHVAZUGXKXE
      XQXEWCWDQUHZWCWDRUHZVAXQWCWDQRVBXRXIXSWHWCWDXCVCABVDVEUSVFXIWHVGUSUTWCWDW
      LWMVHXBXHXJGZXAGXLWGXTXAWGXGXJGZWFGXTWEYAWFWCWDVIVJXGXJWFVKUSVJXHXJXAVLUS
      VMVNWGWHAVOVPVQWQXPWSWDLWOVRXCVSVTWAWB $.
      $( [11-Apr-2012] $) $( [5-Apr-2012] $)
  $}

  $( The ` Bigcup ` relationship is a relationship. $)
  relbigcup $p |- Rel Bigcup $=
    ( cbigcup wrel cvv cxp cep ccom ctxp csymdif crn cdif relxp reldif ax-mp
    df-bigcup releqi mpbir ) ABCCDZEEFCGCEGHIZJZBZQBTCCKQRLMASNOP $.
    $( [11-Apr-2012] $)

  ${
    $d A x y z $.  $d B x y z $.
    brbigcup.1 $e |- B e. _V $.
    $( Binary relationship over ` Bigcup ` . $)
    brbigcup $p |- ( A Bigcup B <-> U. A = B ) $=
      ( vx vy vz cbigcup wbr cvv wcel cuni wceq relbigcup brrelexi eleq1
      mpbiri uniexb sylibr cv breq1 unieq eqeq1d cop cxp cep ccom ctxp csymdif
      crn cdif df-br df-bigcup eleq2i wn wb wal wex opex elrn brsymdif wel wa
      visset brco epel anbi12i exbii bitri brtxp brv mpbiran2 eluni 3bitr4i
      mpbiran epelcNEW bibi12i notbii exnal 3bitrri con1bii eldif opelxp
      mpbir2an dfcleq 3bitri vtoclbg pm5.21nii ) ABGHZAIJZAKZBLZABGMNWKWJIJZWIW
      KWLBIJZCWJBIOPAQRDSZBGHZWNKZBLZWHWKDAIWNABGTWNALWPWJBWNAUAUBWOWNBUCZGJWRI
      IUDZUEUEUFZIUGZIUEUGZUHZUIZUJZJZWQWNBGUKGXEWRULUMWRXDJZUNZESZWPJZXIBJZUOZ
      EUPZXFWQXMXGXGXIWRXCHZEUQXLUNZEUQXMUNEWRXCWNBURUSXNXOEXNXIWRXAHZXIWRXBHZU
      OZUNXOXIWRXAXBUTXRXLXPXJXQXKXIWNWTHZEFVAZFDVAZVBZFUQZXPXJXSXIFSZUEHZYDWNU
      EHZVBZFUQYCFXIWNUEUEEVCZDVCZVDYGYBFYEXTYFYAEFVEFDVEVFVGVHXPXSXIBIHWTIXIWN
      BYHYICVIXIBVJVKFXIWNVLVMXQXIBUEHZXKXQXIWNIHYJIUEXIWNBYHYICVIXIWNVJVNXIBCV
      OVHVPVQVHVGXLEVRVSVTXFWRWSJZXHWRWSXDWAYKWNIJWMWNBIICWBYICWCVNEWPBWDVMWEWF
      WG $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d x y z t $.
    $( ` Bigcup ` using maps-to notation. $)
    dfbigcup2 $p |- Bigcup = ( x e. _V |-> U. x ) $=
      ( vy vz vt cbigcup cvv cv cuni cmpt relbigcup mptrel wceq wbr eqcom
      visset brbigcup wcel wa weq eleq1 unieq eqeq2d anbi12d biantrur syl6bbr
      eqeq1 df-mpt brab 3bitr4i eqbrriv ) BCEAFAGZHZIZJAFULKBGZHZCGZLUPUOLZUNUP
      EMUNUPUMMUOUPNUNUPCOZPUKFQZDGZULLZRZUTUOLZUQADUNUPUMBOZURABSZVBUNFQZVCRVC
      VEUSVFVAVCUKUNFTVEULUOUTUKUNUAUBUCVFVCVDUDUEUTUPUOUFADFULUGUHUIUJ $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` maps the universe onto itself. $)
    fobigcup $p |- Bigcup : _V -onto-> _V $=
      ( vx vy cvv cbigcup wfo wfn crn wceq df-fo cv cuni wcel wral uniexg rgen
      dfbigcup2 mptfng mpbi wrex cab rnmpt visset csn snex unisn eqcomi unieq
      eqeq2d rcla4ev mp2an 2th abbi2i eqtr4i mpbir2an ) CCDEDCFZDGZCHCCDIAJZKZC
      LZACMUOUSACUQCNOACURDAPZQRUPBJZURHZACSZBTCABCURDUTUAVCBCVACLVCBUBZVAUCZCL
      VAVEKZHZVCVAUDVFVAVAVDUEUFVBVGAVECUQVEHURVFVAUQVEUGUHUIUJUKULUMUN $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  ${
    $d x y z $.
    $( ` Bigcup ` is a function over the universal class. $)
    fnbigcup $p |- Bigcup Fn _V $=
      ( cvv cbigcup wfo wfn fobigcup fofn ax-mp ) AABCBADEAABFG $.
      $( [11-Apr-2012] $)
  $}

  ${
    fvbigcup.1 $e |- A e. _V $.
    $( For sets, ` Bigcup ` yields union. $)
    fvbigcup $p |- ( Bigcup ` A ) = U. A $=
      ( cbigcup cfv cuni wceq wbr eqid uniex brbigcup mpbir cvv wfn wcel wb
      fnbigcup fnbrfvb mp2an ) ACDAEZFZASCGZUASSFSHASABIZJKCLMALNTUAOPBLASCUBQR
      K $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix.1 $e |- A e. _V $.
    $( Membership in the fixpoints of a class. $)
    elfix $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cid cin cdm cv wbr wex df-fix eleq2i eldm wceq wa brin
      visset ideq anbi2i bitri exbii breq2 biimparc 19.23aiv eqid eqeq2
      anbi12d cla4ev mpan2 impbii 3bitri ) ABEZFABGHZIZFADJZUOKZDLZAABKZUNUPABM
      NDAUOCOUSAUQBKZAUQPZQZDLZUTURVCDURVAAUQGKZQVCAUQBGRVEVBVAAUQDSTUAUBUCVDUT
      VCUTDVBUTVAAUQABUDUEUFUTAAPZVDAUGVCUTVFQDACUQAPVAUTVBVFUQAABUDUQAAUHUIUJU
      KULUBUM $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x $.  $d R x $.
    elfix2.1 $e |- Rel R $.
    $( Alternative membership in the fixpoint of a class. $)
    elfix2 $p |- ( A e. Fix R <-> A R A ) $=
      ( vx cfix wcel cvv wbr elisset brrelexi cv wb wceq eleq1 breq12 anidms
      bibi12d visset elfix vtoclg pm5.21nii ) ABEZFZAGFAABHZAUBIAABCJDKZUBFZUEU
      EBHZLUCUDLDAGUEAMZUFUCUGUDUEAUBNUHUGUDLUEAUEABOPQUEBDRSTUA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.
    $( The fixpoints of a class in terms of its range. $)
    dffix2 $p |- Fix A = ran ( A i^i _I ) $=
      ( vx vy cfix cid cin crn cv wbr weq wa wex wcel equid visset breq1
      equequ1 anbi12d cla4ev mpan2 biimpac 19.23aiv impbii elfix elrn brin
      ideq anbi2i bitri exbii 3bitr4i eqriv ) BADZAEFZGZBHZUPAIZCHZUPAIZCBJZKZC
      LZUPUMMUPUOMZUQVBUQBBJZVBBNVAUQVDKCUPBOZUTUSUQUTVDURUPUPAPZCBBQRSTVAUQCUT
      USUQVFUAUBUCUPAVEUDVCURUPUNIZCLVBCUPUNVEUEVGVACVGUSURUPEIZKVAURUPAEUFVHUT
      USURUPVEUGUHUIUJUIUKUL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoints of a class are a subset of its domain. $)
  fixssdm $p |- Fix A C_ dom A $=
    ( cfix cid cin cdm df-fix wss inss1 dmss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  $( The fixpoints of a class are a subset of its range. $)
  fixssrn $p |- Fix A C_ ran A $=
    ( cfix cid cin crn dffix2 wss inss1 rnss ax-mp eqsstri ) ABACDZEZAEZAFLAGMN
    GACHLAIJK $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    $( The fixpoints of a class are the same as those of its converse. $)
    fixcnv $p |- Fix A = Fix `' A $=
      ( vx cfix ccnv cv wbr wcel visset brcnv elfix 3bitr4ri eqriv ) BACZADZCZB
      EZPNFPPAFPOGPMGPPABHZQIPNQJPAQJKL $.
      $( [12-May-2012] $) $( [16-Apr-2012] $)
  $}

  $( The fixpoint operator distributes over union. $)
  fixun $p |- Fix ( A u. B ) = ( Fix A u. Fix B ) $=
    ( cun cid cin cdm cfix indir dmeqi dmun eqtri df-fix uneq12i 3eqtr4i ) ABCZ
    DEZFZADEZFZBDEZFZCZOGAGZBGZCQRTCZFUBPUEABDHIRTJKOLUCSUDUAALBLMN $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.
    ellimits.1 $e |- A e. _V $.
    $( Membership in the class of all limit ordinals. $)
    ellimits $p |- ( A e. Limits <-> Lim A ) $=
      ( con0 cbigcup cfix cin wcel c0 csn wn wa word cuni wceq wne climits
      wlim elin elon wbr elfix brbigcup eqcom 3bitri anbi12i bitri elsnc
      necon3bbii cdif df-limits eleq2i eldif w3a df-lim df-3an an32 3bitr4i )
      ACDEZFZGZAHIZGZJZKZALZAAMZNZKZAHOZKZAPGZAQZUTVHVCVIUTACGZAURGZKVHACURRVMV
      EVNVGABSVNAADTVFANVGADBUAAABUBVFAUCUDUEUFVBAHAHBUGUHUEVKAUSVAUIZGVDPVOAUJ
      UKAUSVAULUFVLVEVIVGUMVEVIKVGKVJAUNVEVIVGUOVEVIVGUPUDUQ $.
      $( [11-Apr-2012] $)
  $}

  $( The class of all limit ordinals is a subclass of the class of all
     ordinals. $)
  limitssson $p |- Limits C_ On $=
    ( vx climits con0 wss cv wcel wi dfss2 wlim word limord visset ellimits
    elon 3imtr4i mpgbir ) BCDAEZBFZQCFZGAABCHQIQJRSQKQALZMQTNOP $.
    $( [11-Apr-2012] $)

  ${
    $d x y $.
    $( A quantifier-free definition of ` om ` that does not depend on
       ~ ax-inf . $)
    dfom5 $p |- om = ( On i^i |^| Limits ) $=
      ( vx vy cv word cab wlim wel wi wal cin wa con0 climits cint com inab
      df-on wcel df-int visset ellimits imbi1i albii abbii eqtri ineq12i df-om
      3eqtr4ri ) ACDZAEZBCZFZABGZHZBIZAEZJUIUOKAELMNZJOUIUOAPLUJUQUPAQUQUKMRZUM
      HZBIZAEUPABMSUTUOAUSUNBURULUMUKBTUAUBUCUDUEUFABUGUH $.
      $( [11-Apr-2012] $)
  $}


