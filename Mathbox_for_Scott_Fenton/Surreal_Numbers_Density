$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Density
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Lemma for ~ axdense .  A surreal is a function over its birthday. $)
    axdenselem1 $p |- ( A e. No -> A Fn ( bday ` A ) ) $=
      ( cbday cfv wfn wfun cdm wceq csur wcel df-fn nofun bdayval eqcomd
      sylanbrc ) AABCZDAEAFZOGAHIZAOJAKQOPALMN $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( Lemma for ~ axfe and ~ axdense .  The value of a surreal at its birthday
       is ` (/) ` $)
    axdenselem2 $p |- ( A e. No -> ( A ` ( bday ` A ) ) = (/) ) $=
      ( vx csur wcel cbday cfv cdm c0 bdayval fveq2d wn wceq cv c1o c2o cpr wf
      con0 wrex elno fdm eleq1d biimprd word eloni ordirr syl syl6com
      r19.23aiv sylbi ndmfv eqtrd ) ACDZAEFZAFAGZAFZHUMUNUOAAIJUMUOUODKZUPHLUMB
      MZNOPZAQZBRSUQBATUTUQBRUTURRDZUORDZUQUTVBVAUTUOURRURUSAUAUBUCVBUOUDUQUOUE
      UOUFUGUHUIUJUOAUKUGUL $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  If one surreal is older than another, then there
       is an ordinal at which they are not equal. $)
    axdenselem3 $p |- ( ( A e. No /\ B e. No ) ->
                           ( ( bday ` A ) e. ( bday ` B ) ->
                             E. a e. On ( A ` a ) =/= ( B ` a ) ) ) $=
      ( csur wcel wa cbday cfv cv wne con0 wrex wceq fveq2 neeq1d neeq2d bitrd
      rcla4ev bdayelon c0 wi cdm bdayval eleq2d wfn crn c1o c2o cpr wss
      fnfvelrn ssel nosgnn0 eleq1 mtbiri necon2ai syl6com syl ex com23 wfun
      nofun funfn sylib norn sylc sylbid adantl imp necomd wb axdenselem2
      ad2antrr mpbird sylancr ) ADEZBDEZFZAGHZBGHZEZCIZAHZWBBHZJZCKLZVSKEVSAHZV
      SBHZJZWFVRWAFZWEWICVSKWBVSMZWEWGWDJWIWKWCWGWDWBVSANOWKWDWHWGWBVSBNPQRASWJ
      WITWHJZWJWHTVRWAWHTJZVQWAWMUAVPVQWAVSBUBZEZWMVQVTWNVSBUCUDBWNUEZBUFZUGUHU
      IZUJZWOWMUAVQWPWOWSWMWPWOWSWMUAZWPWOFWHWQEZWTWNVSBUKWSXAWHWREZWMWQWRWHULX
      BWHTWHTMXBTWREUMWHTWRUNUOUPUQURUSUTVQBVAWPBVBBVCVDBVEVFVGVHVIVJVPWIWLVKVQ
      WAVPWGTWHAVLOVMVNVOUS $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  Show that a particular abstraction is an
       ordinal. $)
    axdenselem4 $p |- ( ( ( A e. No /\ B e. No ) /\ A <s B ) ->
                        |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. On ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab c0 cint wrex wceq wn wi
      breq2 biimprcd con3d sltirr syl5com adantr cbday wral wfn wb eqfnfv
      axdenselem1 syl2an notbid wo axdenselem3 necom rexbii syl6ib ancoms jaod
      word bdayelon eloni ax-mp ordtri3 mp2an con2bii syl5ibr rexnal wss onssi
      ssrexv df-ne sylibr sylbir a1i ianor syl5ib sylbid syld imp rabn0 ssrab2
      oninton mpan syl ) ADEZBDEZFZABGHZFZCIZAJZWLBJZKZCLMZNKZWPOLEZWKWOCLPZWQW
      IWJWSWIWJABQZRZWSWGWJXASWHWJAAGHZRXAWGWJWTXBWTXBWJABAGTUAUBAUCUDUEWIXAAUF
      JZBUFJZQZWMWNQZCXCUGZFZRZWSWIWTXHAXCUHBXDUHWTXHUIWGWHCXCXDABUJAUKBUKULUMW
      IXERZXGRZUNWSXIWIXJWSXKWIXCXDEZXDXCEZUNZWSXJWIXLWSXMABCUOWHWGXMWSSWHWGFXM
      WNWMKZCLPWSBACUOXOWOCLWNWMUPUQURUSUTXEXNXCVAZXDVAZXEXNRUIXCLEXPAVBZXCVCVD
      XDLEXQBVBXDVCVDXCXDVEVFVGVHXKWSSWIXKXFRZCXCPZWSXFCXCVIXTXSCLPZWSXCLVJXTYA
      SXCXRVKXSCXCLVLVDWOXSCLWMWNVMUQVNVOVPUTXEXGVQVRVSVTWAWOCLWBVNWPLVJWQWRWOC
      LWCWPWDWEWF $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  If the birthdays of two distinct surreals are
       equal, then the ordinal from ~ axdenselem4 is an element of that
       birthday. $)
    axdenselem5 $p |- ( ( ( A e. No /\ B e. No ) /\
                            ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                          |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } e.
                          ( bday ` A ) ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv wne con0 crab cint cdm wn
      breq2 notbid sltirr syl5cbi con2d imp ad2ant2rl wral wb wfun eqfunfv
      nofun syl2an adantr wi wrex word wss ordtr2 axdenselem4 eloni syl
      nodmord ad2antrr onelon nodmon sylan ex anim1d weq fveq2 neeq1d neeq2d
      bitrd elrab intss1 sylbir simprl syl2anc exp32 r19.23adv df-ne rexbii
      rexnal bitri syl5ibr imnan biimpri impcom syl5 exp4b com23 imp32 sylbid
      mpd bdayval eqeqan12d anbi1d eleq2d 3imtr4d ) AEFZBEFZGZAHIZBHIZJZABKLZGZ
      CMZAIZXHBIZNZCOPZQZXCFZXBARZBRZJZXFGZXMXOFZXGXNXBXRXSXBXRGZABJZSZXSWTXFYB
      XAXQWTXFYBWTYAXFYAAAKLZSXFSWTYAYCXFABAKTUAAUBUCUDUEUFXTYBXQDMZAIZYDBIZJZD
      XOUGZGZSZXSXBYBYJUHXRXBYAYIAUIBUIYAYIUHWTXADABUJAUKBUKULUAUMXBXQXFYJXSUNZ
      XBXFXQYKXBXFXQYJXSXBXFGZYHSZXSXQYJGYLYEYFNZDXOUOZXSYMYLYNXSDXOYLYDXOFZYNX
      SXMUPZXOUPZXMYDUQZYPXSYLYPYNGZGZYQYRGYSYPGXSXMYDXOURUEYLYQYTYLXMOFYQABCUS
      XMUTVAUMXBYRXFYTWTYRXAAVBUMVCUUAYDOFZYNGZYSYLYTUUCYLYPUUBYNWTYPUUBUNXAXFW
      TYPUUBXOOFYPUUBWTXOYDVDAVEVFVGVCVHUEUUCYDXLFYSXKYNCYDOCDVIZXKYEXJNYNUUDXI
      YEXJXHYDAVJVKUUDXJYFYEXHYDBVJVLVMVNYDXLVOVPVAYLYPYNVQVRVSVTYOYGSZDXOUOYMY
      NUUEDXOYEYFWAWBYGDXOWCWDWEYJXQYMXQYMUNYJXQYHWFWGWHWIWJWKWLWMWNVGXBXEXQXFW
      TXAXCXOXDXPAWOZBWOWPWQWTXNXSUHXAWTXCXOXMUUFWRUMWSUE $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( The restriction of a surreal to the abstraction from ~ axdenselem4 is
       still a surreal. $)
    axdenselem6 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( A |` |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } ) e. No ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv c1o c2o cpr wne con0 crab
      cint cres wf wrex feq2 rcla4ev axdenselem4 adantrl wfn crn wss df-f
      fnssres axdenselem1 ad2antrr axdenselem5 bdayelon onelssi syl sylanc
      sstr resss rnss ax-mp norn sylancr sylanbrc elno sylibr ) AEFZBEFZGZAHIZB
      HIJZABKLZGZGZDMZNOPZACMZAIVSBIQCRSTZUAZUBZDRUCZWAEFVTRFZVTVRWAUBZWCVPWBWE
      DVTRVQVTVRWAUDUEVKVNWDVMABCUFUGWEWAVTUHZWAUIZVRUJZVPVTVRWAUKAVLUHZVTVLUJZ
      WFVPVLVTAULVIWIVJVOAUMUNVPVTVLFWJABCUOVLVTAUPUQURUSWGAUIZUJZWKVRUJZWHVPWG
      WKVRUTWAAUJWLAVTVAWAAVBVCVIWMVJVOAVDUNVEVFUSDWAVGVH $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.  $d C a $.
    $( Lemma for ~ axdense . ` A ` and ` B ` are equal at all elements of the
       abstraction. $)
    axdenselem7 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( C e. |^| { a e. On | ( A ` a ) =/= ( B ` a ) }
                          -> ( A ` C ) = ( B ` C ) ) ) $=
      ( csur wcel wa cbday cfv wceq cslt wbr cv wne con0 crab cint wn wi
      axdenselem4 adantrl onelon ex syl wss sylan ontri1 con2bid biimpd com23
      imp mpd intss1 nsyl fveq2 neeq1d neeq2d bitrd elrab notbii syl6ib imnan
      syl6ibr mpdd df-ne con2bii ) AEFBEFGZAHIBHIJZABKLZGGZCDMZAIZVKBIZNZDOPZQZ
      FZCAIZCBIZNZRZVRVSJZVJVQCOFZWAVJVPOFZVQWCSVGVIWDVHABDTUAZWDVQWCVPCUBZUCUD
      VJVQWCVTGZRZWCWASVJVQCVOFZRZWHVJVQWJVJVQGZVPCUEZWIWKWCWLRZWDVQWCVJWFWEUFV
      JVQWCWMSZVJWDVQWNSWEWDWCVQWMWDWCVQWMSWDWCGZVQWMWOWLVQVPCUGUHUIUCUJUDUKULC
      VOUMUNUCWIWGVNVTDCOVKCJZVNVRVMNVTWPVLVRVMVKCAUOUPWPVMVSVRVKCBUOUQURUSUTVA
      WCVTVBVCVDVTWBVRVSVEVFVC $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}


  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  Give a condition for surreal less than when two
       surreals have the same birthday. $)
    axdenselem8 $p |- ( ( A e. No /\ B e. No /\
                            ( bday ` A ) = ( bday ` B ) ) ->
                          ( A <s B <->
                            ( ( A `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              1o /\
                              ( B `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              2o ) ) ) $=
      ( csur wcel cbday cfv wceq w3a cslt wbr cv wne con0 crab cint c1o c2o wa
      wi axdenselem5 exp32 3impia c0 cop ctp wb sltval2 3adant3 w3o wn 3orel13
      eleq2 biimpd cpr nosgnn0 crn wfn eleq1 fnfvelrn syl5cbi axdenselem1
      sylan norn sseld adantr syld mtoi ex adantl syl9r imp intnand 3ad2antl1
      intnanrd sylanc com23 fvex 0ex 2on elisseti brtp syl5ib sylbid mpdd
      3mix2 sylibr syl5bir impbid ) ADEZBDEZAFGZBFGZHZIZABJKZCLZAGWQBGMCNOPZAGZ
      QHZWRBGZRHZSZWOWPWRWLEZXCWJWKWNWPXDTWJWKSZWNWPXDABCUAUBUCWOWPWSXAQUDUEQRU
      EUDRUEUFKZXDXCTZWJWKWPXFUGWNABCUHUIZWOWTXAUDHZSZXCWSUDHZXBSZUJZXGXFWOXDXM
      XCWOXDXMXCTZXJUKXLUKXNWOXDSZXJXCXLULXOXIWTWOXDXIUKZWJWKWNXDXPTWNXDWRWMEZX
      EXPWNXDXQWLWMWRUMUNWKXQXPTWJWKXQXPWKXQSZXIUDQRUOZEZUPXRXIUDBUQZEZXTBWMURZ
      XQXIYBTWKXIXAYAEYBYCXQSXAUDYAUSWMWRBUTVABVBVCWKYBXTTXQWKYAXSUDBVDVEVFVGVH
      VIVJVKUCVLVMXOXKXBWJWKXDXKUKWNWJXDSZXKXTUPYDXKUDAUQZEZXTAWLURZXDXKYFTWJXK
      WSYEEYFYGXDSWSUDYEUSWLWRAUTVAAVBVCWJYFXTTXDWJYEXSUDAVDVEVFVGVHVNVOVPVIVQQ
      UDQRUDRWSXAWRAVRWRBVRVSRNVTWAZYHWBZWCWDWEWOWPXFXCXHXCXMXFXCXJXLWFYIWGWHWI
      $.
      $( [10-Jul-2011] $) $( [19-Jun-2011] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.
    $( Given two distinct surreals with the same birthday, there is an older
       surreal lying between the two of them.  Alling's axiom (SD) $)
    axdense $p |- ( ( ( A e. No /\ B e. No ) /\
                      ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                    E. x e. No ( ( bday ` x ) e. ( bday ` A ) /\
                                 A <s x /\ x <s B ) ) $=
      ( va vy cv cfv wne con0 crab cint cres csur wcel cbday cslt wbr w3a wrex
      wa wceq fveq2 eleq1d breq2 breq1 3anbi123d rcla4ev axdenselem6 cdm
      bdayval syl cin wss axdenselem5 bdayelon onelssi ad2antrr sseqtrd df-ss
      sylib dmres syl5eq eqtrd eqeltrd wral c1o c0 cop c2o ctp raleq breq12d
      anbi12d axdenselem4 adantrl w3o wi axdenselem8 biimpd 3expia imp32
      pm3.26d eqid jctir 3mix1d fvex 0ex 2on elisseti brtp sylibr fveq2d
      axdenselem2 eqtr3d breqtrrd fvres eqcomd rgen jctil sylanc wb sltval
      simpll mpbird adantl axdenselem7 imp r19.21aiva pm3.27d 3mix3d eqbrtrd
      sylan32c simplr syl3an2c ) BDFZBGXOCGHDIJKZLZMNZXQOGZBOGZNZBXQPQZXQCPQZAF
      ZOGZXTNZBYDPQZYDCPQZRZAMSBMNZCMNZTZXTCOGUAZBCPQZTZTZYIYAYBYCRAXQMYDXQUAZY
      FYAYGYBYHYCYQYEXSXTYDXQOUBUCYDXQBPUDYDXQCPUEUFUGBCDUHZYPXSXPXTYPXSXQUIZXP
      YPXRXSYSUAYRXQUJUKYPXPBUIZULZXPYSYPXPYTUMUUAXPUAYPXPXTYTYPXPXTNXPXTUMBCDU
      NZXTXPBUOUPUKYJXTYTUAYKYOBUJUQURXPYTUSUTBXPVAVBVCZUUBVDYPYBEFZBGZUUDXQGZU
      AZEYDVEZYDBGZYDXQGZVFVGVHVFVIVHVGVIVHVJZQZTZAISZXPINZUUGEXPVEZXPBGZXPXQGZ
      UUKQZTZUUNYPUUMUUTAXPIYDXPUAZUUHUUPUULUUSUUGEYDXPVKUVAUUIUUQUUJUURUUKYDXP
      BUBYDXPXQUBZVLVMUGYLYNUUOYMBCDVNVOZYPUUSUUPYPUUQVGUURUUKYPUUQVFUAZVGVGUAZ
      TZUVDVGVIUAZTZUUQVGUAUVGTZVPUUQVGUUKQYPUVFUVHUVIYPUVDUVEYPUVDXPCGZVIUAZYL
      YMYNUVDUVKTZYJYKYMYNUVLVQYJYKYMRYNUVLBCDVRVSVTWAZWBVGWCZWDWEVFVGVFVIVGVIU
      UQVGXPBWFWGWGVIIWHWIZUVOWJWKYPXSXQGZUURVGYPXSXPXQUUCWLYPXRUVPVGUAYRXQWMUK
      WNZWOUUGEXPUUDXPNZUUFUUEUUDXPBWPWQZWRWSWTYJXRYBUUNXAYPAEBXQXBYJYKYOXCYRWT
      XDYPYCUUFUUDCGZUAZEYDVEZUUJYDCGZUUKQZTZAISZUUOUWAEXPVEZUURUVJUUKQZUWFYPUV
      CYPUWAEXPYPUVRTUUEUUFUVTUVRUUGYPUVSXEYPUVRUUEUVTUABCUUDDXFXGWNXHYPUURVGUV
      JUUKUVQYPVGVFUAZUVJVGUATZUWIUVKTZUVEUVKTZVPVGUVJUUKQYPUWLUWJUWKYPUVKUVEYP
      UVDUVKUVMXIUVNWSXJVFVGVFVIVGVIVGUVJWGXPCWFWGUVOUVOWJWKXKUWEUWGUWHTAXPIUVA
      UWBUWGUWDUWHUWAEYDXPVKUVAUUJUURUWCUVJUUKUVBYDXPCUBVLVMUGXLXRYKYCUWFXAYPAE
      XQCXBYRYJYKYOXMWTXDXN $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A w x y z $.  $d X w x y z $.  $d Y w y z $.
    $( Lemma for ~ nocvxmin .  Given two birthday-minimal elements of a convex
       class of surreals, they are not comparable. $)
    nocvxminlem $p |- ( ( A C_ No /\ A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                         ( ( ( X e. A /\ Y e. A ) /\
                             ( ( bday ` X ) = |^| ( bday " A ) /\
                               ( bday ` Y ) = |^| ( bday " A ) ) ) ->
                           -. X <s Y ) ) $=
      ( vw csur wss cv cslt wbr wa wcel wi wral cbday cfv cima cint wceq wn
      w3a wrex breq1 anbi1d imbi1d ralbidv breq2 anbi2d rcla42v weq anbi12d
      eleq1 imbi12d rcla4v cdm bdaydm sseq2i wfun bdayfun funfvima2 mpan
      sylbir imp intss1 syl con0 wb ontri1 c0 wne oninton crn imassrn bdayrn
      sseqtri ne0i sylancr bdayelon sylancl mpbid ex eleq2 notbid biimprcd
      syl6 com3l adantrd syl8 com35 com4l impcom imp42 con2d 3anass notbii
      imnan bitr4i sylibr nrexdv axdense anassrs ssel anim12d eqtr3 anim12i
      anasss adantlr sylan mtand ) DHIZAJZCJZKLZXNBJZKLZMZXNDNZOZCHPZBDPADPZMZE
      DNZFDNZMZEQRZQDSZTZUAZFQRZYIUAZMZMZEFKLZUBYCYNMZYOGJZQRZYGNZEYQKLZYQFKLZU
      CZGHUDZYPUUBGHYPYQHNZMZYSYTUUAMZUBOZUUBUBZUUEUUFYSYCYFYMUUDUUFYSUBZOZYBXL
      YFYMUUDUUJOOZOYFYBXLUUKYFYBEXNKLZXNFKLZMZXSOZCHPZXLUUKOYAUUPUULXQMZXSOZCH
      PABEFDDXMEUAZXTUURCHUUSXRUUQXSUUSXOUULXQXMEXNKUEUFUGUHXPFUAZUURUUOCHUUTUU
      QUUNXSUUTXQUUMUULXPFXNKUIUJUGUHUKUUDUUPXLYMUUJUUDUUPUUFYMXLUUIUUDUUPUUFYQ
      DNZYMXLUUIOZOUUOUUFUVAOCYQHCGULZUUNUUFXSUVAUVCUULYTUUMUUAXNYQEKUIXNYQFKUE
      UMXNYQDUNUOUPUVAYJUVBYLXLUVAYJUUIXLUVAYRYINZUBZYJUUIOXLUVAUVEXLUVAMZYIYRI
      ZUVEUVFYRYHNZUVGXLUVAUVHXLDQUQZIZUVAUVHOZUVIHDURUSQUTUVJUVKVADYQQVBVCVDVE
      ZYRYHVFVGYIVHNZYRVHNUVGUVEVIUVFYIYRVJYHVHIYHVKVLZUVMUVFYHVMYHQVNVHQDVOVPV
      QUVFUVHUVNUVLYHYRVRVGVSYQVTWAWBWCYJUUIUVEYJYSUVDYGYIYRWDWEWFWGWHWIWJWKWLW
      GWHWMWNWOUUHYSUUFMZUBUUGUUBUVOYSYTUUAWPWQYSUUFWRWSWTXAEHNZFHNZMZYGYKUAZMZ
      YOUUCYPUVRUVSYOUUCGEFXBXCXLYNUVTYBXLYFYMUVTXLYFMUVRYMUVSXLYFUVRXLYDUVPYEU
      VQDHEXDDHFXDXEVEYGYKYIXFXGXHXIXJXKWC $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)

  $}

  ${
    $d A t w x y z $.
    $( Given a non-empty convex class of surreals, there is a unique
       birthday-minimal element of that class. $)
    nocvxmin $p |- ( ( A =/= (/) /\ A C_ No /\
                        A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                      E! w e. A ( bday ` w ) = |^| ( bday " A ) ) $=
      ( vt cv cbday cfv cima cint wceq wreu wrex wa weq wi wral c0 wne csur
      wss cslt wbr wcel w3a fveq2 eqeq1d reu4 con0 onint crn imassrn bdayrn
      sseqtri wex cdm bdaydm sseq2i wfun bdayfun funfvima2 mpan sylbir fvex
      eleq1 cla4ev syl6 19.23adv n0 3imtr4g impcom sylancr wb wfn bdayfn
      fvelimab adantl mpbid 3adant3 wn ssel anim12d imp ad2ant2r nocvxminlem
      ancom anbi12i syl5ib wor axsltso sotrieq2 biimpar sylan32c exp32
      r19.21aivv 3adant1 sylanbrc ) DGZHIZHEJZKZLZDEMXCDENZXCFGZHIZXBLZOZDFPZQZ
      FERDERZESTZEUAUBZAGZCGZUCUDXOBGUCUDOXOEUEQCUARBERAERZUFXCXGDFEXIWTXFXBWSX
      EHUGUHUIXLXMXDXPXLXMOZXBXAUEZXDXAUJUBXASTZXRXQXAUKXAHULUJHEUMUNUOXMXLXSXM
      XNEUEZAUPWSXAUEZDUPZXLXSXMXTYBAXMXTXNHIZXAUEZYBXMEHUQZUBZXTYDQZYEUAEURUSH
      UTYFYGVAEXNHVBVCVDYAYDDYCXNHVEWSYCXAVFVGVHVIAEVJDXAVJVKVLVMXMXRXDVNZXLHUA
      VOXMYHVPDUAEXBHVQVCVRVSVTXMXPXKXLXMXPOZXJDFEEYIWSEUEZXEEUEZOZXHXIWSUAUEZX
      EUAUEZOZWSXEUCUDWAZXEWSUCUDWAZXIYIYLXHOZOXMYLYOXPXHXMYLYOXMYJYMYKYNEUAWSW
      BEUAXEWBWCWDWEYIYRYPABCEWSXEWFWDYIYRYQYIYKYJOZXGXCOZOYQYRABCEXEWSWFYLYSXH
      YTYJYKWGXCXGWGWHWIWDYOXIYPYQOZUAUCWJYOXIUUAVNWKUAWSXEUCWLVCWMWNWOWPWQWR
      $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)
  $}

