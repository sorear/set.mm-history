$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Density
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Lemma for ~ axdense .  A surreal is a function over its birthday. $)
    axdenselem1 $p |- ( A e. No -> A Fn ( bday ` A ) ) $=
      ( csur wcel wfun cdm cbday cfv wceq wfn nofun bdayval eqcomd df-fn
      sylanbrc ) ABCZADAEZAFGZHAQIAJOQPAKLAQMN $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( Lemma for axfe (future) and ~ axdense .  The value of a surreal at its
       birthday is ` (/) ` .  (Shortened proof on 2012-Apr-14, SF) $)
    axdenselem2 $p |- ( A e. No -> ( A ` ( bday ` A ) ) = (/) ) $=
      ( csur wcel cbday cfv cdm wn c0 wceq bdayelon onirri bdayval eleq2d
      mtbii ndmfv syl ) ABCZADEZAFZCZGRAEHIQRRCTRAJKQRSRALMNRAOP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  If one surreal is older than another, then there
       is an ordinal at which they are not equal. $)
    axdenselem3 $p |- ( ( A e. No /\ B e. No ) ->
                           ( ( bday ` A ) e. ( bday ` B ) ->
                             E. a e. On ( A ` a ) =/= ( B ` a ) ) ) $=
      ( csur wcel wa cbday cfv cdm cv wne con0 wrex wceq bdayval adantl eleq2d
      bdayelon c0 wn c1o c2o cpr nosgnn0 crn wss norn adantr wfun nofun fvelrn
      sylan sseldd eleq1 syl5ibcom mtoi df-ne sylibr adantll axdenselem2
      ad2antrr neeqtrrd necomd fveq2 neeq12d rcla4ev sylancr ex sylbid ) ADEZBD
      EZFZAGHZBGHZEVMBIZEZCJZAHZVQBHZKZCLMZVLVNVOVMVKVNVONVJBOPQVLVPWAVLVPFZVML
      EVMAHZVMBHZKZWAARWBWDWCWBWDSWCVKVPWDSKZVJVKVPFZWDSNZTWFWGWHSUAUBUCZEZUDWG
      WDWIEWHWJWGBUEZWIWDVKWKWIUFVPBUGUHVKBUIVPWDWKEBUJVMBUKULUMWDSWIUNUOUPWDSU
      QURUSVJWCSNVKVPAUTVAVBVCVTWECVMLVQVMNVRWCVSWDVQVMAVDVQVMBVDVEVFVGVHVI $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  Show that a particular abstraction is an
       ordinal. $)
    axdenselem4 $p |- ( ( ( A e. No /\ B e. No ) /\ A <s B ) ->
                        |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. On ) $=
      ( csur wcel wa cslt wbr cv cfv wne con0 crab wss c0 cint ssrab2 wrex
      wceq wn wi sltirr breq2 biimprcd con3d syl5com adantr cbday wral wfn wb
      axdenselem1 eqfnfv syl2an notbid wo ianor word bdayelon eloni ax-mp
      ordtri3 mp2an con2bii axdenselem3 necom rexbii syl6ib ancoms jaod
      syl5bir rexnal onssi ssrexv df-ne sylibr sylbir a1i syl5bi sylbid syld
      imp rabn0 oninton sylancr ) ADEZBDEZFZABGHZFZCIZAJZWKBJZKZCLMZLNWOOKZWOPL
      EWNCLQWJWNCLRZWPWHWIWQWHWIABSZTZWQWFWIWSUAWGWFAAGHZTWIWSAUBWIWRWTWRWTWIAB
      AGUCUDUEUFUGWHWSAUHJZBUHJZSZWLWMSZCXAUIZFZTZWQWHWRXFWFAXAUJBXBUJWRXFUKWGA
      ULBULCXAXBABUMUNUOXGXCTZXETZUPWHWQXCXEUQWHXHWQXIXHXAXBEZXBXAEZUPZWHWQXCXL
      XAURZXBURZXCXLTUKXALEXMAUSZXAUTVAXBLEXNBUSXBUTVAXAXBVBVCVDWHXJWQXKABCVEWG
      WFXKWQUAWGWFFXKWMWLKZCLRWQBACVEXPWNCLWMWLVFVGVHVIVJVKXIWQUAWHXIXDTZCXARZW
      QXDCXAVLXRXQCLRZWQXALNXRXSUAXAXOVMXQCXALVNVAWNXQCLWLWMVOVGVPVQVRVJVSVTWAW
      BWNCLWCVPWOWDWE $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  If the birthdays of two distinct surreals are
       equal, then the ordinal from ~ axdenselem4 is an element of that
       birthday. $)
    axdenselem5 $p |- ( ( ( A e. No /\ B e. No ) /\
                            ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                          |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } e.
                          ( bday ` A ) ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv wne con0 crab cint cdm wn
      sltirr breq2 notbid syl5ibcom con2d imp ad2ant2rl wral wb wfun nofun
      eqfunfv syl2an adantr wi imnan biimpri impcom wrex df-ne rexbii rexnal
      bitri word wss axdenselem4 eloni syl nodmord ad3antrrr nodmon onelon
      sylan ex ad2antrr anim1d weq fveq2 neeq12d elrab intss1 sylbir simprl
      ordtr2 syl22anc exp32 r19.23adv syl5bir syl5 exp4b com23 imp32 sylbid
      mpd bdayval eqeqan12d anbi1d eleq2d 3imtr4d ) AEFZBEFZGZAHIZBHIZJZABKLZGZ
      CMZAIZXGBIZNZCOPZQZXBFZXAARZBRZJZXEGZXLXNFZXFXMXAXQXRXAXQGZABJZSZXRWSXEYA
      WTXPWSXEYAWSXTXEWSAAKLZSXTXESATXTYBXEABAKUAUBUCUDUEUFXSYAXPDMZAIZYCBIZJZD
      XNUGZGZSZXRXAYAYIUHXQXAXTYHWSAUIBUIXTYHUHWTAUJBUJDABUKULUBUMXAXPXEYIXRUNZ
      XAXEXPYJXAXEXPYIXRXPYIGYGSZXAXEGZXRYIXPYKXPYKUNYIXPYGUOUPUQYKYDYENZDXNURZ
      YLXRYNYFSZDXNURYKYMYODXNYDYEUSUTYFDXNVAVBYLYMXRDXNYLYCXNFZYMXRYLYPYMGZGZX
      LVCZXNVCZXLYCVDZYPXRYLYSYQYLXLOFYSABCVEXLVFVGUMWSYTWTXEYQAVHVIYRYCOFZYMGZ
      UUAYLYQUUCYLYPUUBYMWSYPUUBUNWTXEWSYPUUBWSXNOFYPUUBAVJXNYCVKVLVMVNVOUEUUCY
      CXKFUUAXJYMCYCOCDVPXHYDXIYEXGYCAVQXGYCBVQVRVSYCXKVTWAVGYLYPYMWBYSYTGUUAYP
      GXRXLYCXNWCUEWDWEWFWGWHWIWJWKWLWMVMXAXDXPXEWSWTXBXNXCXOAWNZBWNWOWPWSXMXRU
      HWTWSXBXNXLUUDWQUMWRUE $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( The restriction of a surreal to the abstraction from ~ axdenselem4 is
       still a surreal. $)
    axdenselem6 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( A |` |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } ) e. No ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv c1o c2o cpr wne con0 crab
      cint cres wf wrex axdenselem4 adantrl wfn crn wss axdenselem1 ad2antrr
      axdenselem5 bdayelon onelssi syl fnssres syl11anc resss rnss ax-mp norn
      sstr sylancr df-f sylanbrc feq2 rcla4ev elno sylibr ) AEFZBEFZGZAHIZBHIJZ
      ABKLZGZGZDMZNOPZACMZAIVSBIQCRSTZUAZUBZDRUCZWAEFVPVTRFZVTVRWAUBZWCVKVNWDVM
      ABCUDUEVPWAVTUFZWAUGZVRUHZWEVPAVLUFZVTVLUHZWFVIWIVJVOAUIUJVPVTVLFWJABCUKV
      LVTAULUMUNVLVTAUOUPVPWGAUGZUHZWKVRUHZWHWAAUHWLAVTUQWAAURUSVIWMVJVOAUTUJWG
      WKVRVAVBVTVRWAVCVDWBWEDVTRVQVTVRWAVEVFUPDWAVGVH $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.  $d C a $.
    $( Lemma for ~ axdense . ` A ` and ` B ` are equal at all elements of the
       abstraction. $)
    axdenselem7 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( C e. |^| { a e. On | ( A ` a ) =/= ( B ` a ) }
                          -> ( A ` C ) = ( B ` C ) ) ) $=
      ( csur wcel wa cbday cfv wceq cslt wbr cv wne con0 crab cint wn wi
      axdenselem4 adantrl onelon ex syl wss sylan ontri1 con2bid biimpd com23
      imp mpd intss1 nsyl fveq2 neeq12d elrab notbii syl6ib imnan syl6ibr mpdd
      df-ne con2bii ) AEFBEFGZAHIBHIJZABKLZGGZCDMZAIZVIBIZNZDOPZQZFZCAIZCBIZNZR
      ZVPVQJZVHVOCOFZVSVHVNOFZVOWASVEVGWBVFABDTUAZWBVOWAVNCUBZUCUDVHVOWAVRGZRZW
      AVSSVHVOCVMFZRZWFVHVOWHVHVOGZVNCUEZWGWIWAWJRZVHWBVOWAWCWDUFVHVOWAWKSZVHWB
      VOWLSWCWBWAVOWKWBWAVOWKSWBWAGZVOWKWMWJVOVNCUGUHUIUCUJUDUKULCVMUMUNUCWGWEV
      LVRDCOVICJVJVPVKVQVICAUOVICBUOUPUQURUSWAVRUTVAVBVRVTVPVQVCVDVA $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}


  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  Give a condition for surreal less than when two
       surreals have the same birthday. $)
    axdenselem8 $p |- ( ( A e. No /\ B e. No /\
                            ( bday ` A ) = ( bday ` B ) ) ->
                          ( A <s B <->
                            ( ( A `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              1o /\
                              ( B `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              2o ) ) ) $=
      ( csur wcel cbday cfv wceq w3a cslt wbr cv wne con0 crab cint c1o c2o wa
      wi axdenselem5 exp32 3impia c0 cop ctp wb sltval2 3adant3 w3o fvex 0ex
      2on elisseti brtp wn eleq2 biimpd cpr nosgnn0 crn wfn axdenselem1
      fnfvelrn eleq1 syl5ibcom sylan norn sseld adantr syld mtoi ex adantl
      syl9r imp intnand 3ad2antl1 intnanrd 3orel13 syl11anc com23 syl5bi
      sylbid mpdd 3mix2 sylibr syl5ibr impbid ) ADEZBDEZAFGZBFGZHZIZABJKZCLZAGW
      QBGMCNOPZAGZQHZWRBGZRHZSZWOWPWRWLEZXCWJWKWNWPXDTWJWKSZWNWPXDABCUAUBUCWOWP
      WSXAQUDUEQRUEUDRUEUFKZXDXCTZWJWKWPXFUGWNABCUHUIZXFWTXAUDHZSZXCWSUDHZXBSZU
      JZWOXGQUDQRUDRWSXAWRAUKWRBUKULRNUMUNZXNUOZWOXDXMXCWOXDXMXCTZWOXDSZXJUPXLU
      PXPXQXIWTWOXDXIUPZWJWKWNXDXRTWNXDWRWMEZXEXRWNXDXSWLWMWRUQURWKXSXRTWJWKXSX
      RWKXSSZXIUDQRUSZEZUTXTXIUDBVAZEZYBWKBWMVBZXSXIYDTBVCYEXSSXAYCEXIYDWMWRBVD
      XAUDYCVEVFVGWKYDYBTXSWKYCYAUDBVHVIVJVKVLVMVNVOUCVPVQXQXKXBWJWKXDXKUPWNWJX
      DSZXKYBUTYFXKUDAVAZEZYBWJAWLVBZXDXKYHTAVCYIXDSWSYGEXKYHWLWRAVDWSUDYGVEVFV
      GWJYHYBTXDWJYGYAUDAVHVIVJVKVLVRVSXJXCXLVTWAVMWBWCWDWEXCWPWOXFXCXMXFXCXJXL
      WFXOWGXHWHWI $.
      $( [10-Jul-2011] $) $( [19-Jun-2011] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.
    $( Given two distinct surreals with the same birthday, there is an older
       surreal lying between the two of them.  Alling's axiom (SD) $)
    axdense $p |- ( ( ( A e. No /\ B e. No ) /\
                      ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                    E. x e. No ( ( bday ` x ) e. ( bday ` A ) /\
                                 A <s x /\ x <s B ) ) $=
      ( va vy csur wcel wa cbday cfv wceq cslt wbr cv wne con0 crab cint cres
      w3a wrex axdenselem6 cdm bdayval syl cin dmres wss axdenselem5 bdayelon
      onelssi ad2antrr sseqtrd df-ss sylib syl5eq eqtrd eqeltrd wral c1o c0
      cop c2o ctp axdenselem4 adantrl w3o wi axdenselem8 biimpd 3expia imp32
      simpld eqid jctir 3mix1d fvex 0ex 2on elisseti brtp sylibr fveq2d
      axdenselem2 eqtr3d breqtrrd fvres eqcomd rgen jctil raleq fveq2 breq12d
      anbi12d rcla4ev syl11anc wb simpll sltval mpbird adantl axdenselem7 imp
      r19.21aiva simprd 3mix3d eqbrtrd syl12anc simplr eleq1d breq2 breq1
      3anbi123d syl13anc ) BFGZCFGZHZBIJZCIJKZBCLMZHZHZBDNZBJYCCJODPQRZSZFGZYEI
      JZXRGZBYELMZYECLMZANZIJZXRGZBYKLMZYKCLMZTZAFUABCDUBZYBYGYDXRYBYGYEUCZYDYB
      YFYGYRKYQYEUDUEYBYRYDBUCZUFZYDBYDUGYBYDYSUHYTYDKYBYDXRYSYBYDXRGYDXRUHBCDU
      IZXRYDBUJUKUEXOXRYSKXPYABUDULUMYDYSUNUOUPUQZUUAURYBYIENZBJZUUCYEJZKZEYKUS
      ZYKBJZYKYEJZUTVAVBUTVCVBVAVCVBVDZMZHZAPUAZYBYDPGZUUFEYDUSZYDBJZYDYEJZUUJM
      ZHZUUMXQXTUUNXSBCDVEVFZYBUURUUOYBUUPVAUUQUUJYBUUPUTKZVAVAKZHZUVAVAVCKZHZU
      UPVAKUVDHZVGUUPVAUUJMYBUVCUVEUVFYBUVAUVBYBUVAYDCJZVCKZXQXSXTUVAUVHHZXOXPX
      SXTUVIVHXOXPXSTXTUVIBCDVIVJVKVLZVMVAVNZVOVPUTVAUTVCVAVCUUPVAYDBVQVRVRVCPV
      SVTZUVLWAWBYBYGYEJZUUQVAYBYGYDYEUUBWCYBYFUVMVAKYQYEWDUEWEZWFUUFEYDUUCYDGZ
      UUEUUDUUCYDBWGWHZWIWJUULUUSAYDPYKYDKZUUGUUOUUKUURUUFEYKYDWKUVQUUHUUPUUIUU
      QUUJYKYDBWLYKYDYEWLZWMWNWOWPYBXOYFYIUUMWQXOXPYAWRYQAEBYEWSWPWTYBYJUUEUUCC
      JZKZEYKUSZUUIYKCJZUUJMZHZAPUAZYBUUNUVTEYDUSZUUQUVGUUJMZUWEUUTYBUVTEYDYBUV
      OHUUDUUEUVSUVOUUFYBUVPXAYBUVOUUDUVSKBCUUCDXBXCWEXDYBUUQVAUVGUUJUVNYBVAUTK
      ZUVGVAKHZUWHUVHHZUVBUVHHZVGVAUVGUUJMYBUWKUWIUWJYBUVHUVBYBUVAUVHUVJXEUVKWJ
      XFUTVAUTVCVAVCVAUVGVRYDCVQVRUVLUVLWAWBXGUWDUWFUWGHAYDPUVQUWAUWFUWCUWGUVTE
      YKYDWKUVQUUIUUQUWBUVGUUJUVRYKYDCWLWMWNWOXHYBYFXPYJUWEWQYQXOXPYAXIAEYECWSW
      PWTYPYHYIYJTAYEFYKYEKZYMYHYNYIYOYJUWLYLYGXRYKYEIWLXJYKYEBLXKYKYECLXLXMWOX
      N $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A w x y z $.  $d X w x y z $.  $d Y w y z $.
    $( Lemma for ~ nocvxmin .  Given two birthday-minimal elements of a convex
       class of surreals, they are not comparable. $)
    nocvxminlem $p |- ( ( A C_ No /\ A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                         ( ( ( X e. A /\ Y e. A ) /\
                             ( ( bday ` X ) = |^| ( bday " A ) /\
                               ( bday ` Y ) = |^| ( bday " A ) ) ) ->
                           -. X <s Y ) ) $=
      ( vw csur wss cv cslt wbr wa wcel wi wral cbday cfv cima cint wceq wn
      w3a wrex breq1 anbi1d imbi1d ralbidv breq2 anbi2d rcla42v weq anbi12d
      eleq1 imbi12d rcla4v cdm bdaydm sseq2i wfun bdayfun funfvima2 mpan
      sylbir imp intss1 syl con0 wb c0 wne crn imassrn bdayrn sseqtri ne0i
      oninton sylancr bdayelon ontri1 sylancl mpbid ex eleq2 notbid biimprcd
      syl6 com3l adantrd syl8 com35 com4l impcom imp42 con2d 3anass notbii
      imnan bitr4i sylibr nrexdv ssel anim12d eqtr3 anim12i anasss adantlr
      axdense anassrs sylan mtand ) DHIZAJZCJZKLZXNBJZKLZMZXNDNZOZCHPZBDPADPZMZ
      EDNZFDNZMZEQRZQDSZTZUAZFQRZYIUAZMZMZEFKLZUBYCYNMZYOGJZQRZYGNZEYQKLZYQFKLZ
      UCZGHUDZYPUUBGHYPYQHNZMZYSYTUUAMZUBOZUUBUBZUUEUUFYSYCYFYMUUDUUFYSUBZOZYBX
      LYFYMUUDUUJOOZOYFYBXLUUKYFYBEXNKLZXNFKLZMZXSOZCHPZXLUUKOYAUUPUULXQMZXSOZC
      HPABEFDDXMEUAZXTUURCHUUSXRUUQXSUUSXOUULXQXMEXNKUEUFUGUHXPFUAZUURUUOCHUUTU
      UQUUNXSUUTXQUUMUULXPFXNKUIUJUGUHUKUUDUUPXLYMUUJUUDUUPUUFYMXLUUIUUDUUPUUFY
      QDNZYMXLUUIOZOUUOUUFUVAOCYQHCGULZUUNUUFXSUVAUVCUULYTUUMUUAXNYQEKUIXNYQFKU
      EUMXNYQDUNUOUPUVAYJUVBYLXLUVAYJUUIXLUVAYRYINZUBZYJUUIOXLUVAUVEXLUVAMZYIYR
      IZUVEUVFYRYHNZUVGXLUVAUVHXLDQUQZIZUVAUVHOZUVIHDURUSQUTUVJUVKVADYQQVBVCVDV
      EZYRYHVFVGUVFYIVHNZYRVHNUVGUVEVIUVFYHVHIYHVJVKZUVMYHQVLVHQDVMVNVOUVFUVHUV
      NUVLYHYRVPVGYHVQVRYQVSYIYRVTWAWBWCYJUUIUVEYJYSUVDYGYIYRWDWEWFWGWHWIWJWKWL
      WGWHWMWNWOUUHYSUUFMZUBUUGUUBUVOYSYTUUAWPWQYSUUFWRWSWTXAYPEHNZFHNZMZYGYKUA
      ZMZYOUUCXLYNUVTYBXLYFYMUVTXLYFMUVRYMUVSXLYFUVRXLYDUVPYEUVQDHEXBDHFXBXCVEY
      GYKYIXDXEXFXGUVRUVSYOUUCGEFXHXIXJXKWC $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)

  $}

  ${
    $d A t w x y z $.
    $( Given a non-empty convex class of surreals, there is a unique
       birthday-minimal element of that class. $)
    nocvxmin $p |- ( ( A =/= (/) /\ A C_ No /\
                        A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                      E! w e. A ( bday ` w ) = |^| ( bday " A ) ) $=
      ( vt c0 wne csur wss cv cslt wbr wa wcel wi wral w3a cbday cfv cima cint
      wceq wrex weq wreu con0 crn imassrn bdayrn sseqtri wex cdm bdaydm sseq2i
      wfun bdayfun funfvima2 mpan sylbir fvex eleq1 cla4ev syl6 19.23adv n0
      3imtr4g impcom onint sylancr wb wfn bdayfn fvelimab adantl mpbid 3adant3
      wn ssel anim12d imp ad2ant2r nocvxminlem ancom anbi12i syl5bi wor
      axsltso sotrieq2 biimpar syl12anc exp32 r19.21aivv 3adant1 fveq2 eqeq1d
      reu4 sylanbrc ) EGHZEIJZAKZCKZLMXBBKLMNXBEOPCIQBEQAEQZRDKZSTZSEUAZUBZUCZD
      EUDZXHFKZSTZXGUCZNZDFUEZPZFEQDEQZXHDEUFWSWTXIXCWSWTNZXGXFOZXIXQXFUGJXFGHZ
      XRXFSUHUGSEUIUJUKWTWSXSWTXAEOZAULXDXFOZDULZWSXSWTXTYBAWTXTXASTZXFOZYBWTES
      UMZJZXTYDPZYEIEUNUOSUPYFYGUQEXASURUSUTYAYDDYCXASVAXDYCXFVBVCVDVEAEVFDXFVF
      VGVHXFVIVJWTXRXIVKZWSSIVLWTYHVMDIEXGSVNUSVOVPVQWTXCXPWSWTXCNZXODFEEYIXDEO
      ZXJEOZNZXMXNYIYLXMNZNXDIOZXJIOZNZXDXJLMVRZXJXDLMVRZXNWTYLYPXCXMWTYLYPWTYJ
      YNYKYOEIXDVSEIXJVSVTWAWBYIYMYQABCEXDXJWCWAYIYMYRYMYKYJNZXLXHNZNYIYRYLYSXM
      YTYJYKWDXHXLWDWEABCEXJXDWCWFWAYPXNYQYRNZILWGYPXNUUAVKWHIXDXJLWIUSWJWKWLWM
      WNXHXLDFEXNXEXKXGXDXJSWOWPWQWR $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)
  $}

