$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Density
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $( Lemma for ~ axdense .  A surreal is a function over its birthday. $)
    axdenselem1 $p |- ( A e. No -> A Fn ( bday ` A ) ) $=
      ( csur wcel wfun cdm cbday cfv wceq nofun bdayval eqcomd df-fn sylanbrc
      wfn ) ABCZADAEZAFGZHAQNAIOQPAJKAQLM $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A x $.
    $( Lemma for axfe (future) and ~ axdense .  The value of a surreal at its
       birthday is ` (/) ` .  (Shortened proof on 2012-Apr-14, SF) $)
    axdenselem2 $p |- ( A e. No -> ( A ` ( bday ` A ) ) = (/) ) $=
      ( csur wcel cbday cfv cdm wn c0 wceq bdayelon onirri bdayval eleq2d mtbii
      ndmfv syl ) ABCZADEZAFZCZGRAEHIQRRCTRAJKQRSRALMNRAOP $.
      $( [10-Jul-2011] $) $( [14-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  If one surreal is older than another, then there
       is an ordinal at which they are not equal. $)
    axdenselem3 $p |- ( ( A e. No /\ B e. No ) ->
                           ( ( bday ` A ) e. ( bday ` B ) ->
                             E. a e. On ( A ` a ) =/= ( B ` a ) ) ) $=
      ( csur wcel wa cbday cfv cdm cv wne con0 wrex wceq adantl eleq2d bdayelon
      bdayval c0 fveq2 wn c1o c2o cpr nosgnn0 crn norn adantr wfun nofun fvelrn
      wss sylan sseldd eleq1 syl5ibcom mtoi sylibr adantll axdenselem2 ad2antrr
      df-ne neeqtrrd necomd neeq12d rcla4ev sylancr ex sylbid ) ADEZBDEZFZAGHZB
      GHZEVMBIZEZCJZAHZVQBHZKZCLMZVLVNVOVMVKVNVONVJBROPVLVPWAVLVPFZVMLEVMAHZVMB
      HZKZWAAQWBWDWCWBWDSWCVKVPWDSKZVJVKVPFZWDSNZUAWFWGWHSUBUCUDZEZUEWGWDWIEWHW
      JWGBUFZWIWDVKWKWIULVPBUGUHVKBUIVPWDWKEBUJVMBUKUMUNWDSWIUOUPUQWDSVBURUSVJW
      CSNVKVPAUTVAVCVDVTWECVMLVQVMNVRWCVSWDVQVMATVQVMBTVEVFVGVHVI $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  Show that a particular abstraction is an
       ordinal. $)
    axdenselem4 $p |- ( ( ( A e. No /\ B e. No ) /\ A <s B ) ->
                        |^| { a e. On | ( A ` a ) =/= ( B ` a ) } e. On ) $=
      ( csur wcel wa cslt wbr cfv wne con0 wss wrex wceq wn wi cbday wfn ax-mp
      wb cv crab c0 cint ssrab2 sltirr breq2 biimprcd con3d syl5com adantr wral
      axdenselem1 eqfnfv syl2an ianor word bdayelon eloni ordtri3 mp2an con2bii
      notbid wo axdenselem3 necom rexbii syl6ib ancoms jaod rexnal onssi ssrexv
      syl5bir df-ne sylibr sylbir a1i syl5bi sylbid syld rabn0 oninton sylancr
      imp ) ADEZBDEZFZABGHZFZCUAZAIZWKBIZJZCKUBZKLWOUCJZWOUDKEWNCKUEWJWNCKMZWPW
      HWIWQWHWIABNZOZWQWFWIWSPWGWFAAGHZOWIWSAUFWIWRWTWRWTWIABAGUGUHUIUJUKWHWSAQ
      IZBQIZNZWLWMNZCXAULZFZOZWQWHWRXFWFAXARBXBRWRXFTWGAUMBUMCXAXBABUNUOVCXGXCO
      ZXEOZVDWHWQXCXEUPWHXHWQXIXHXAXBEZXBXAEZVDZWHWQXCXLXAUQZXBUQZXCXLOTXAKEXMA
      URZXAUSSXBKEXNBURXBUSSXAXBUTVAVBWHXJWQXKABCVEWGWFXKWQPWGWFFXKWMWLJZCKMWQB
      ACVEXPWNCKWMWLVFVGVHVIVJVNXIWQPWHXIXDOZCXAMZWQXDCXAVKXRXQCKMZWQXAKLXRXSPX
      AXOVLXQCXAKVMSWNXQCKWLWMVOVGVPVQVRVJVSVTWAWEWNCKWBVPWOWCWD $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( Lemma for ~ axdense .  If the birthdays of two distinct surreals are
       equal, then the ordinal from ~ axdenselem4 is an element of that
       birthday. $)
    axdenselem5 $p |- ( ( ( A e. No /\ B e. No ) /\
                            ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                          |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } e.
                          ( bday ` A ) ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv wne con0 wn imp wb adantr wi
      crab cint sltirr breq2 notbid syl5ibcom con2d ad2ant2rl wral wfun eqfunfv
      cdm nofun syl2an imnan biimpri impcom wrex df-ne rexbii rexnal bitri word
      wss axdenselem4 eloni syl nodmord ad3antrrr nodmon onelon sylan ex anim1d
      ad2antrr fveq2 neeq12d elrab intss1 sylbir simprl syl22anc exp32 rexlimdv
      weq ordtr2 syl5bir syl5 exp4b com23 imp32 sylbid bdayval eqeqan12d anbi1d
      mpd eleq2d 3imtr4d ) AEFZBEFZGZAHIZBHIZJZABKLZGZCMZAIZXGBIZNZCOUAZUBZXBFZ
      XAAULZBULZJZXEGZXLXNFZXFXMXAXQXRXAXQGZABJZPZXRWSXEYAWTXPWSXEYAWSXTXEWSAAK
      LZPXTXEPAUCXTYBXEABAKUDUEUFUGQUHXSYAXPDMZAIZYCBIZJZDXNUIZGZPZXRXAYAYIRXQX
      AXTYHWSAUJBUJXTYHRWTAUMBUMDABUKUNUESXAXPXEYIXRTZXAXEXPYJXAXEXPYIXRXPYIGYG
      PZXAXEGZXRYIXPYKXPYKTYIXPYGUOUPUQYKYDYENZDXNURZYLXRYNYFPZDXNURYKYMYODXNYD
      YEUSUTYFDXNVAVBYLYMXRDXNYLYCXNFZYMXRYLYPYMGZGZXLVCZXNVCZXLYCVDZYPXRYLYSYQ
      YLXLOFYSABCVEXLVFVGSWSYTWTXEYQAVHVIYRYCOFZYMGZUUAYLYQUUCYLYPUUBYMWSYPUUBT
      WTXEWSYPUUBWSXNOFYPUUBAVJXNYCVKVLVMVOVNQUUCYCXKFUUAXJYMCYCOCDWEXHYDXIYEXG
      YCAVPXGYCBVPVQVRYCXKVSVTVGYLYPYMWAYSYTGUUAYPGXRXLYCXNWFQWBWCWDWGWHWIWJWKW
      LWPVMXAXDXPXEWSWTXBXNXCXOAWMZBWMWNWOWSXMXRRWTWSXBXNXLUUDWQSWRQ $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a x $.  $d B a x $.
    $( The restriction of a surreal to the abstraction from ~ axdenselem4 is
       still a surreal.  (Contributed by Scott Fenton, 16-Jun-2011.) $)
    axdenselem6 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( A |` |^| { a e. On | (
                                     A ` a ) =/= ( B ` a ) } ) e. No ) $=
      ( vx csur wcel wa cbday cfv wceq cslt wbr cv con0 wf wfn crn wss ad2antrr
      syl11anc c1o c2o cpr wne crab axdenselem4 adantrl axdenselem1 axdenselem5
      cint cres wrex bdayelon onelssi syl fnssres resss rnss ax-mp norn sylancr
      sstr df-f sylanbrc feq2 rcla4ev elno sylibr ) AEFZBEFZGZAHIZBHIJZABKLZGZG
      ZDMZUAUBUCZACMZAIVSBIUDCNUEUJZUKZOZDNULZWAEFVPVTNFZVTVRWAOZWCVKVNWDVMABCU
      FUGVPWAVTPZWAQZVRRZWEVPAVLPZVTVLRZWFVIWIVJVOAUHSVPVTVLFWJABCUIVLVTAUMUNUO
      VLVTAUPTVPWGAQZRZWKVRRZWHWAARWLAVTUQWAAURUSVIWMVJVOAUTSWGWKVRVBVAVTVRWAVC
      VDWBWEDVTNVQVTVRWAVEVFTDWAVGVH $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A a $.  $d B a $.  $d C a $.
    $( Lemma for ~ axdense . ` A ` and ` B ` are equal at all elements of the
       abstraction. $)
    axdenselem7 $p |- ( ( ( A e. No /\ B e. No ) /\
                        ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                        ( C e. |^| { a e. On | ( A ` a ) =/= ( B ` a ) }
                          -> ( A ` C ) = ( B ` C ) ) ) $=
      ( csur wcel wa cbday cfv wceq cslt wbr wne con0 wn wi syl fveq2 syl6ibr
      ex cv crab cint axdenselem4 adantrl onelon wss sylan ontri1 con2bid com23
      biimpd imp mpd intss1 nsyl neeq12d elrab notbii syl6ib imnan mpdd con2bii
      df-ne ) AEFBEFGZAHIBHIJZABKLZGGZCDUAZAIZVIBIZMZDNUBZUCZFZCAIZCBIZMZOZVPVQ
      JZVHVOCNFZVSVHVNNFZVOWAPVEVGWBVFABDUDUEZWBVOWAVNCUFZTQVHVOWAVRGZOZWAVSPVH
      VOCVMFZOZWFVHVOWHVHVOGZVNCUGZWGWIWAWJOZVHWBVOWAWCWDUHVHVOWAWKPZVHWBVOWLPW
      CWBWAVOWKWBWAVOWKPWBWAGZVOWKWMWJVOVNCUIUJULTUKQUMUNCVMUOUPTWGWEVLVRDCNVIC
      JVJVPVKVQVICARVICBRUQURUSUTWAVRVASVBVRVTVPVQVDVCS $.
      $( [10-Jul-2011] $) $( [17-Jun-2011] $)
  $}


  ${
    $d A a $.  $d B a $.
    $( Lemma for ~ axdense .  Give a condition for surreal less than when two
       surreals have the same birthday. $)
    axdenselem8 $p |- ( ( A e. No /\ B e. No /\
                            ( bday ` A ) = ( bday ` B ) ) ->
                          ( A <s B <->
                            ( ( A `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              1o /\
                              ( B `
                                |^| { a e. On | ( A ` a ) =/= ( B ` a ) } ) =
                              2o ) ) ) $=
      ( csur wcel cbday cfv wceq wbr con0 c1o c2o wa wi 3impia cop fvex nosgnn0
      c0 wn w3a cslt cv wne crab cint axdenselem5 exp32 ctp sltval2 3adant3 w3o
      0ex 2on elisseti brtp eleq2 biimpd cpr crn wfn axdenselem1 fnfvelrn eleq1
      wb syl5ibcom sylan norn sseld adantr syld mtoi adantl syl9r imp 3ad2antl1
      intnand intnanrd 3orel13 syl11anc com23 syl5bi sylbid mpdd sylibr syl5ibr
      ex 3mix2 impbid ) ADEZBDEZAFGZBFGZHZUAZABUBIZCUCZAGWQBGUDCJUEUFZAGZKHZWRB
      GZLHZMZWOWPWRWLEZXCWJWKWNWPXDNWJWKMZWNWPXDABCUGUHOWOWPWSXAKSPKLPSLPUIIZXD
      XCNZWJWKWPXFVEWNABCUJUKZXFWTXASHZMZXCWSSHZXBMZULZWOXGKSKLSLWSXAWRAQWRBQUM
      LJUNUOZXNUPZWOXDXMXCWOXDXMXCNZWOXDMZXJTXLTXPXQXIWTWOXDXITZWJWKWNXDXRNWNXD
      WRWMEZXEXRWNXDXSWLWMWRUQURWKXSXRNWJWKXSXRWKXSMZXISKLUSZEZRXTXISBUTZEZYBWK
      BWMVAZXSXIYDNBVBYEXSMXAYCEXIYDWMWRBVCXASYCVDVFVGWKYDYBNXSWKYCYASBVHVIVJVK
      VLWGVMVNOVOVQXQXKXBWJWKXDXKTWNWJXDMZXKYBRYFXKSAUTZEZYBWJAWLVAZXDXKYHNAVBY
      IXDMWSYGEXKYHWLWRAVCWSSYGVDVFVGWJYHYBNXDWJYGYASAVHVIVJVKVLVPVRXJXCXLVSVTW
      GWAWBWCWDXCWPWOXFXCXMXFXCXJXLWHXOWEXHWFWI $.
      $( [10-Jul-2011] $) $( [19-Jun-2011] $)
  $}

  ${
    $d A a x y $.  $d B a x y $.
    $( Given two distinct surreals with the same birthday, there is an older
       surreal lying between the two of them.  Alling's axiom (SD) (Contributed
       by Scott Fenton, 16-Jun-2011.) $)
    axdense $p |- ( ( ( A e. No /\ B e. No ) /\
                      ( ( bday ` A ) = ( bday ` B ) /\ A <s B ) ) ->
                    E. x e. No ( ( bday ` x ) e. ( bday ` A ) /\
                                 A <s x /\ x <s B ) ) $=
      ( va vy csur wcel wa cbday cfv wceq cslt wbr con0 wral c1o c2o 0ex fveq2
      c0 cv wne crab cint cres w3a wrex axdenselem6 cdm bdayval syl axdenselem5
      cin dmres wss bdayelon onelssi ad2antrr sseqtrd df-ss sylib eqtrd eqeltrd
      syl5eq cop ctp axdenselem4 adantrl w3o wi axdenselem8 biimpd 3expia imp32
      simpld eqid jctir 3mix1d fvex 2on elisseti brtp sylibr fveq2d axdenselem2
      eqtr3d breqtrrd fvres eqcomd jctil raleq breq12d anbi12d rcla4ev syl11anc
      rgen simpll sltval mpbird adantl axdenselem7 imp ralrimiva simprd eqbrtrd
      wb 3mix3d syl12anc simplr eleq1d breq2 breq1 3anbi123d syl13anc ) BFGZCFG
      ZHZBIJZCIJKZBCLMZHZHZBDUAZBJYCCJUBDNUCUDZUEZFGZYEIJZXRGZBYELMZYECLMZAUAZI
      JZXRGZBYKLMZYKCLMZUFZAFUGBCDUHZYBYGYDXRYBYGYEUIZYDYBYFYGYRKYQYEUJUKYBYRYD
      BUIZUMZYDBYDUNYBYDYSUOYTYDKYBYDXRYSYBYDXRGYDXRUOBCDULZXRYDBUPUQUKXOXRYSKX
      PYABUJURUSYDYSUTVAVDVBZUUAVCYBYIEUAZBJZUUCYEJZKZEYKOZYKBJZYKYEJZPTVEPQVET
      QVEVFZMZHZANUGZYBYDNGZUUFEYDOZYDBJZYDYEJZUUJMZHZUUMXQXTUUNXSBCDVGVHZYBUUR
      UUOYBUUPTUUQUUJYBUUPPKZTTKZHZUVATQKZHZUUPTKUVDHZVIUUPTUUJMYBUVCUVEUVFYBUV
      AUVBYBUVAYDCJZQKZXQXSXTUVAUVHHZXOXPXSXTUVIVJXOXPXSUFXTUVIBCDVKVLVMVNZVOTV
      PZVQVRPTPQTQUUPTYDBVSRRQNVTWAZUVLWBWCYBYGYEJZUUQTYBYGYDYEUUBWDYBYFUVMTKYQ
      YEWEUKWFZWGUUFEYDUUCYDGZUUEUUDUUCYDBWHWIZWPWJUULUUSAYDNYKYDKZUUGUUOUUKUUR
      UUFEYKYDWKUVQUUHUUPUUIUUQUUJYKYDBSYKYDYESZWLWMWNWOYBXOYFYIUUMXFXOXPYAWQYQ
      AEBYEWRWOWSYBYJUUEUUCCJZKZEYKOZUUIYKCJZUUJMZHZANUGZYBUUNUVTEYDOZUUQUVGUUJ
      MZUWEUUTYBUVTEYDYBUVOHUUDUUEUVSUVOUUFYBUVPWTYBUVOUUDUVSKBCUUCDXAXBWFXCYBU
      UQTUVGUUJUVNYBTPKZUVGTKHZUWHUVHHZUVBUVHHZVITUVGUUJMYBUWKUWIUWJYBUVHUVBYBU
      VAUVHUVJXDUVKWJXGPTPQTQTUVGRYDCVSRUVLUVLWBWCXEUWDUWFUWGHAYDNUVQUWAUWFUWCU
      WGUVTEYKYDWKUVQUUIUUQUWBUVGUUJUVRYKYDCSWLWMWNXHYBYFXPYJUWEXFYQXOXPYAXIAEY
      ECWRWOWSYPYHYIYJUFAYEFYKYEKZYMYHYNYIYOYJUWLYLYGXRYKYEISXJYKYEBLXKYKYECLXL
      XMWNXN $.
      $( [10-Jul-2011] $) $( [16-Jun-2011] $)
  $}

  ${
    $d A w x y z $.  $d X w x y z $.  $d Y w y z $.
    $( Lemma for ~ nocvxmin .  Given two birthday-minimal elements of a convex
       class of surreals, they are not comparable. $)
    nocvxminlem $p |- ( ( A C_ No /\ A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                         ( ( ( X e. A /\ Y e. A ) /\
                             ( ( bday ` X ) = |^| ( bday " A ) /\
                               ( bday ` Y ) = |^| ( bday " A ) ) ) ->
                           -. X <s Y ) ) $=
      ( vw csur wss cv cslt wbr wa wcel wi wral cbday wceq wn con0 cfv cima w3a
      cint breq1 anbi1d imbi1d ralbidv breq2 anbi2d rcla42v weq anbi12d imbi12d
      wrex eleq1 rcla4v bdaydm sseq2i wfun bdayfun funfvima2 mpan sylbir intss1
      cdm imp syl wb c0 wne crn imassrn bdayrn sseqtri oninton sylancr bdayelon
      ne0i ontri1 sylancl mpbid ex eleq2 biimprcd syl6 com3l adantrd syl8 com35
      notbid com4l impcom imp42 con2d 3anass notbii imnan bitr4i sylibr anim12d
      nrexdv ssel eqtr3 anim12i anasss adantlr axdense anassrs sylan mtand ) DH
      IZAJZCJZKLZXNBJZKLZMZXNDNZOZCHPZBDPADPZMZEDNZFDNZMZEQUAZQDUBZUDZRZFQUAZYI
      RZMZMZEFKLZSYCYNMZYOGJZQUAZYGNZEYQKLZYQFKLZUCZGHUOZYPUUBGHYPYQHNZMZYSYTUU
      AMZSOZUUBSZUUEUUFYSYCYFYMUUDUUFYSSZOZYBXLYFYMUUDUUJOOZOYFYBXLUUKYFYBEXNKL
      ZXNFKLZMZXSOZCHPZXLUUKOYAUUPUULXQMZXSOZCHPABEFDDXMERZXTUURCHUUSXRUUQXSUUS
      XOUULXQXMEXNKUEUFUGUHXPFRZUURUUOCHUUTUUQUUNXSUUTXQUUMUULXPFXNKUIUJUGUHUKU
      UDUUPXLYMUUJUUDUUPUUFYMXLUUIUUDUUPUUFYQDNZYMXLUUIOZOUUOUUFUVAOCYQHCGULZUU
      NUUFXSUVAUVCUULYTUUMUUAXNYQEKUIXNYQFKUEUMXNYQDUPUNUQUVAYJUVBYLXLUVAYJUUIX
      LUVAYRYINZSZYJUUIOXLUVAUVEXLUVAMZYIYRIZUVEUVFYRYHNZUVGXLUVAUVHXLDQVFZIZUV
      AUVHOZUVIHDURUSQUTUVJUVKVADYQQVBVCVDVGZYRYHVEVHUVFYITNZYRTNUVGUVEVIUVFYHT
      IYHVJVKZUVMYHQVLTQDVMVNVOUVFUVHUVNUVLYHYRVSVHYHVPVQYQVRYIYRVTWAWBWCYJUUIU
      VEYJYSUVDYGYIYRWDWKWEWFWGWHWIWJWLWFWGWMWNWOUUHYSUUFMZSUUGUUBUVOYSYTUUAWPW
      QYSUUFWRWSWTXBYPEHNZFHNZMZYGYKRZMZYOUUCXLYNUVTYBXLYFYMUVTXLYFMUVRYMUVSXLY
      FUVRXLYDUVPYEUVQDHEXCDHFXCXAVGYGYKYIXDXEXFXGUVRUVSYOUUCGEFXHXIXJXKWC $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)

  $}

  ${
    $d A t w x y z $.
    $( Given a non-empty convex class of surreals, there is a unique
       birthday-minimal element of that class.  (Contributed by Scott Fenton,
       30-Jun-2011.) $)
    nocvxmin $p |- ( ( A =/= (/) /\ A C_ No /\
                        A. x e. A A. y e. A
                           A. z e. No ( ( x <s z /\ z <s y ) -> z e. A ) ) ->
                      E! w e. A ( bday ` w ) = |^| ( bday " A ) ) $=
      ( vt c0 csur wss cv cslt wbr wa wcel wi wral cbday cfv mpan imp cima cint
      wne w3a wceq wrex weq wreu con0 crn imassrn bdayrn sseqtri wex cdm bdaydm
      sseq2i wfun bdayfun funfvima2 sylbir fvex eleq1 cla4ev exlimdv n0 3imtr4g
      syl6 impcom onint sylancr wb bdayfn fvelimab adantl mpbid 3adant3 wn ssel
      wfn anim12d ad2ant2r nocvxminlem ancom anbi12i syl5bi wor axsltso biimpar
      sotrieq2 syl12anc exp32 ralrimivv 3adant1 fveq2 eqeq1d reu4 sylanbrc ) EG
      UCZEHIZAJZCJZKLXBBJKLMXBENOCHPBEPAEPZUDDJZQRZQEUAZUBZUEZDEUFZXHFJZQRZXGUE
      ZMZDFUGZOZFEPDEPZXHDEUHWSWTXIXCWSWTMZXGXFNZXIXQXFUIIXFGUCZXRXFQUJUIQEUKUL
      UMWTWSXSWTXAENZAUNXDXFNZDUNZWSXSWTXTYBAWTXTXAQRZXFNZYBWTEQUOZIZXTYDOZYEHE
      UPUQQURYFYGUSEXAQUTSVAYAYDDYCXAQVBXDYCXFVCVDVHVEAEVFDXFVFVGVIXFVJVKWTXRXI
      VLZWSQHVTWTYHVMDHEXGQVNSVOVPVQWTXCXPWSWTXCMZXODFEEYIXDENZXJENZMZXMXNYIYLX
      MMZMXDHNZXJHNZMZXDXJKLVRZXJXDKLVRZXNWTYLYPXCXMWTYLYPWTYJYNYKYOEHXDVSEHXJV
      SWATWBYIYMYQABCEXDXJWCTYIYMYRYMYKYJMZXLXHMZMYIYRYLYSXMYTYJYKWDXHXLWDWEABC
      EXJXDWCWFTYPXNYQYRMZHKWGYPXNUUAVLWHHXDXJKWJSWIWKWLWMWNXHXLDFEXNXEXKXGXDXJ
      QWOWPWQWR $.
      $( [10-Jul-2011] $) $( [30-Jun-2011] $)
  $}

