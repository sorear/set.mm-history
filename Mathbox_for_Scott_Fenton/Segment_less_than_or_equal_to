$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Segment less than or equal to
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Seg<_ $.

  $( Declare the constant for the segment less than or equal to
     relationship. $)
  csegle $a class Seg<_ $.

  ${
    $d p q n a b c d y $.
    $( Define the segment length comparison relationship.  This relationship
       expresses that the segment ` A B ` is no longer than ` C D ` .  In this
       section, we establish various properties of this relationship showing
       that it is a transitive, reflexive relationship on pairs of points that
       is substitutive under congruence.  Definition 5.4 of [Schwabhauser],
       p. 41. $)
    df-segle $a |- Seg<_ = { <. p , q >. |
       E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
       E. d e. ( EE ` n ) ( p = <. a , b >. /\ q = <. c , d >. /\
         E. y e. ( EE ` n ) ( y Btwn <. c , d >. /\
                              <. a , b >. Cgr <. c , y >. ) ) } $.
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a c $.  $d A c $.  $d a d $.  $d A d $.
    $d a n $.  $d A n $.  $d a p $.  $d A p $.  $d a q $.  $d A q $.  $d a y $.
    $d A y $.  $d B a $.  $d B b $.  $d b c $.  $d B c $.  $d b d $.  $d B d $.
    $d b n $.  $d B n $.  $d b p $.  $d B p $.  $d b q $.  $d B q $.  $d b y $.
    $d B y $.  $d C a $.  $d C b $.  $d C c $.  $d c d $.  $d C d $.  $d c n $.
    $d C n $.  $d c p $.  $d C p $.  $d c q $.  $d C q $.  $d c y $.  $d C y $.
    $d D a $.  $d D b $.  $d D c $.  $d D d $.  $d d n $.  $d D n $.  $d d p $.
    $d D p $.  $d d q $.  $d D q $.  $d d y $.  $d D y $.  $d N a $.  $d N b $.
    $d N c $.  $d N d $.  $d N n $.  $d n p $.  $d n q $.  $d n y $.  $d N y $.
    $d p q $.  $d p y $.  $d q y $.
    $( Binary relationship form of the segment comparison relationship.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    brsegle $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( <. A , B >. Seg<_ <. C , D >. <->
          E. y e. ( EE ` N ) ( y Btwn <. C , D >. /\
                  <. A , B >. Cgr <. C , y >. ) ) ) $=
      ( va vb vc vd vn vp vq cop csegle wbr cv wceq cbtwn ccgr wa cee cfv wrex
      w3a cn wcel opex eqeq1 eqcom syl6bb 3anbi1d rexbidv 2rexbidv 3anbi2d
      df-segle brab wb simp2r visset opthg syl simp3r 3anbi12d wi simpl2l
      ad2antrl eleenn simprlr simprll adantl axdimuniq syl22anc fveq2d rexeqdv
      exbiri anassrs eleq1 bi2anan9 anbi2d opeq12 breq1d breq2d opeq1 adantr
      anbi12d sylan9bb imbi1d 3imtr4d com12 exp3a 3impd expr r19.23advv ex
      r19.23adva sylbid simpl1 simpl2r simpl3l simpl3r eqidd simpr eqeq1d
      3anbi23d opeq2 anbi1d rcla42ev syl113anc 3anbi13d syl111anc fveq2
      3anbi3d rexeqbidv rcla4ev syl11anc impbid syl5bb ) BCNZDENZOPGQZHQZNZXSRZ
      IQZJQZNZXTRZAQZYGSPZYCYEYINZTPZUAZAKQZUBUCZUDZUEZJYOUDZIYOUDZHYOUDZGYOUDZ
      KUFUDZFUFUGZBFUBUCZUGZCUUDUGZUAZDUUDUGZEUUDUGZUAZUEZYIXTSPZXSDYINZTPZUAZA
      UUDUDZLQZYCRZMQZYGRZYPUEZJYOUDZIYOUDHYOUDZGYOUDKUFUDYDUUTYPUEZJYOUDZIYOUD
      HYOUDZGYOUDKUFUDUUBLMXSXTOBCUHDEUHUUQXSRZUVCUVFKGUFYOUVGUVBUVEHIYOYOUVGUV
      AUVDJYOUVGUURYDUUTYPUVGUURXSYCRYDUUQXSYCUIXSYCUJUKULUMUNUNUUSXTRZUVFYTKGU
      FYOUVHUVEYRHIYOYOUVHUVDYQJYOUVHUUTYHYDYPUVHUUTXTYGRYHUUSXTYGUIXTYGUJUKUOU
      MUNUNAKMLGHIJUPUQUUKUUBUUPUUKUUBYABRZYBCRZUAZYEDRZYFERZUAZYPUEZJYOUDIYOUD
      ZHYOUDGYOUDZKUFUDUUPUUKUUAUVQKUFUUKYSUVPGHYOYOUUKYQUVOIJYOYOUUKYDUVKYHUVN
      YPUUKUUFYDUVKURUUCUUEUUFUUJUSYAYBBCUUDGUTHUTVAVBUUKUUIYHUVNURUUCUUGUUHUUI
      VCYEYFDEUUDIUTJUTVAVBVDUNUNUMUUKUVQUUPKUFUUKYNUFUGZUAZUVPUUPGHYOYOUVSYAYO
      UGZYBYOUGZUAZUVPUUPVEUVSUWBUAUVOUUPIJYOYOUVSUWBYEYOUGZYFYOUGZUAZUVOUUPVEU
      VSUWBUWEUAZUAZUVKUVNYPUUPUWGUVKUVNYPUUPVEZUVKUVNUAZUWGUWHUWIUVSBYOUGZCYOU
      GZUAZDYOUGZEYOUGZUAZUAZUAZUUOAYOUDZUUPVEZUWGUWHUVIUVJUVNUWQUWSVEUVIUVJUVN
      UAUAZUWQUUPUWRUWTUWQUAZUUOAUUDYOUXAFYNUBUXAUUCUUEUVRUWJFYNRUXAUUEUUCUVSUU
      EUWTUWPUUEUUFUUCUUJUVRVFVGZBFVHVBUXBUWTUUKUVRUWPVIUWQUWJUWTUVSUWJUWKUWOVJ
      VKBYNFVLVMVNVOVPVQUWIUWFUWPUVSUVKUWBUWLUVNUWEUWOUVIUVTUWJUVJUWAUWKYABYOVR
      YBCYOVRVSUVLUWCUWMUVMUWDUWNYEDYOVRYFEYOVRVSVSVTUWIYPUWRUUPUWIYMUUOAYOUVKY
      MYJXSYKTPZUAZUVNUUOUVKYLUXCYJUVKYCXSYKTYAYBBCWAWBVTUVNYJUULUXCUUNUVNYGXTY
      ISYEYFDEWAWCUVLUXCUUNURUVMUVLYKUUMXSTYEDYIWDWCZWEWFWGUMWHWIWJWKWLWMWNWOWN
      WPWQUUKUUPUUBUUKUUPUAZUUCYDYHYMAUUDUDZUEZJUUDUDZIUUDUDZHUUDUDZGUUDUDZUUBU
      UCUUGUUJUUPWRUXFUUEUUFXSXSRZYHUXDAUUDUDZUEZJUUDUDIUUDUDZUXLUUEUUFUUCUUJUU
      PVFUUEUUFUUCUUJUUPWSUXFUUHUUIUXMXTXTRZUUPUXPUUHUUIUUCUUGUUPWTUUHUUIUUCUUG
      UUPXAUXFXSXBUXFXTXBUUKUUPXCUXOUXMUXQUUPUEUXMDYFNZXTRZYIUXRSPZUUNUAZAUUDUD
      ZUEIJDEUUDUUDUVLYHUXSUXNUYBUXMUVLYGUXRXTYEDYFWDZXDUVLUXDUYAAUUDUVLYJUXTUX
      CUUNUVLYGUXRYISUYCWCUXEWFUMXEUVMUXSUXQUYBUUPUXMUVMUXRXTXTYFEDXFZXDUVMUYAU
      UOAUUDUVMUXTUULUUNUVMUXRXTYISUYDWCXGUMXEXHXIUXJUXPBYBNZXSRZYHYJUYEYKTPZUA
      ZAUUDUDZUEZJUUDUDIUUDUDGHBCUUDUUDUVIUXHUYJIJUUDUUDUVIYDUYFUXGUYIYHUVIYCUY
      EXSYABYBWDZXDUVIYMUYHAUUDUVIYLUYGYJUVIYCUYEYKTUYKWBVTUMXJUNUVJUYJUXOIJUUD
      UUDUVJUYFUXMUYIUXNYHUVJUYEXSXSYBCBXFZXDUVJUYHUXDAUUDUVJUYGUXCYJUVJUYEXSYK
      TUYLWBVTUMXJUNXHXKUUAUXLKFUFYNFRZYTUXKGYOUUDYNFUBXLZUYMYSUXJHYOUUDUYNUYMY
      RUXIIYOUUDUYNUYMYQUXHJYOUUDUYNUYMYPUXGYDYHUYMYMAYOUUDUYNVOXMXNXNXNXNXOXPW
      OXQXR $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N x y $.  $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.
    $( Alternate characterization of segment comparison.  Theorem 5.5 of
       [Schwabhauser], p. 41-42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    brsegle2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( <. A , B >. Seg<_ <. C , D >. <->
          E. x e. ( EE ` N ) ( B Btwn <. A , x >. /\
                  <. A , x >. Cgr <. C , D >. ) ) ) $=
      ( vy cn wcel cee cfv wa w3a cop csegle wbr cv cbtwn ccgr wrex brsegle
      ccgr3 ccolin simprl wi simpl1 simpl3l simpl3r simpr btwncolinear1
      colinearperm1 sylibd syl13anc adantr mpd simpl2l simpl2r simprr
      cgrcomand simpl2 lineext syl131anc mp2and an32 wb simpll1 simplr brcgr3
      syl133anc simp2l simp3 3ad2ant1 mpbird btwnxfr simp32 cgrcom syl122anc
      mpbid jca 3expia sylbid sylanb an32s reximdva ex r19.23adva simpl3 3jca
      syl113anc simp2r simp33 cgrcomlr impbid bitrd ) FHIZBFJKZIZCWPIZLZDWPIZEW
      PIZLZMZBCNZDENZOPGQZXERPZXDDXFNZSPZLZGWPTZCBAQZNZRPZXMXESPZLZAWPTZGBCDEFU
      AXCXKXQXCXJXQGWPXCXFWPIZLZXJXQXSXJLZDXFENZNZBCXLNZNZUBPZAWPTZXQXTDYAUCPZX
      HXDSPZYFXTXGYGXSXGXIUDXSXGYGUEZXJXSWOWTXAXRYIWOWSXBXRUFZWTXAWOWSXRUGZWTXA
      WOWSXRUHZXCXRUIZWOWTXAXRMLXGDEXFNZUCPYGDEXFFUJDEXFFUKULUMUNUOXSXJBCDXFFYJ
      WQWRWOXBXRUPWQWRWOXBXRUQYKYMXSXGXIURUSXSYGYHLYFUEZXJXSWOWTXRXAWSYOYJYKYMY
      LWOWSXBXRUTDXFEBACFVAVBUNVCXTYEXPAWPXSXLWPIZXJYEXPUEZXSYPLXCYPLZXRLZXJYQX
      CXRYPVDYSXJLYEYHXEXMSPZYAYCSPZMZXPYSYEUUBVEZXJYSWOWTXRXAWQWRYPUUCWOWSXBYP
      XRVFZYRWTXRWTXAWOWSYPUGUNZYRXRUIZYRXAXRWTXAWOWSYPUHUNZYRWQXRWQWRWOXBYPUPZ
      UNZYRWRXRWQWRWOXBYPUQZUNZXCYPXRVGZDXFEBCXLFVHVIZUNYSXJUUBXPYSXJUUBMZXNXOU
      UNXGYEXNYSXGXIUUBVJUUNYEUUBYSXJUUBVKYSXJUUCUUBUUMVLVMYSXJXGYELXNUEZUUBYSW
      OWTXRXAWQWRYPUUOUUDUUEUUFUUGUUIUUKUULDXFEBCXLFVNVIVLVCUUNYTXOYSXJYHYTUUAV
      OYSXJYTXOVEZUUBYSWOWTXAWQYPUUPUUDUUEUUGUUIUULDEBXLFVPVQVLVRVSVTWAWBWCWDUO
      WEWFXCXPXKAWPYRXPXKYRXPLZBXLCNZNDYNNUBPZGWPTZXKUUQBUURUCPZXOUUTUUQXNUVAYR
      XNXOUDUUQWOWQYPWRXNUVAUEWOWSXBYPXPVFYRWQXPUUHUNXCYPXPVGYRWRXPUUJUNBXLCFUJ
      UMUOYRXNXOURYRUVAXOLUUTUEZXPYRWOWQYPWRXBUVBWOWSXBYPUFUUHXCYPUIZUUJWOWSXBY
      PWGBXLCDGEFVAVBUNVCUUQUUSXJGWPYRXRXPUUSXJUEYSXPLUUSXOXIUURYNSPZMZXJYSUUSU
      VEVEZXPYSWOWQYPWRMZWTXAXRUVFUUDYRUVGXRYRWQYPWRUUHUVCUUJWHUNUUEUUGUUFBXLCD
      EXFFVHWIUNYSXPUVEXJYSXPUVEMZXGXIUVHXNYDYBUBPZXGYSXNXOUVEVJUVHUVIXIXOYCYAS
      PZMZUVHXIXOUVJYSXPXOXIUVDVOZYSXNXOUVEWJUVHUVDUVJYSXPXOXIUVDWKYSXPUVDUVJVE
      ZUVEYSWOYPWRXAXRUVMUUDUULUUKUUGUUFXLCEXFFWLVQVLVRWHYSXPUVIUVKVEZUVEYSWOWQ
      WRYPWTXRXAUVNUUDUUIUUKUULUUEUUFUUGBCXLDXFEFVHVIVLVMYSXPXNUVILXGUEZUVEYSWO
      WQWRYPWTXRXAUVOUUDUUIUUKUULUUEUUFUUGBCXLDXFEFVNVIVLVCUVLVSVTWAWCWDUOWEWFW
      MWN $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y z $.  $d A y z $.  $d B y z $.  $d C y z $.  $d D y z $.
    $d E y z $.  $d F y z $.  $d G y z $.  $d H y z $.
    $( Substitution law for segment comparison under congruence.  Theorem 5.6
       of [Schwabhauser], p. 42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    seglecgr12 $p |- (
       ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
         ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. Cgr <. E , F >. /\ <. C , D >. Cgr <. G , H >. /\
           <. A , B >. Seg<_ <. C , D >. ) ->
           <. E , F >. Seg<_ <. G , H >. ) ) $=
      ( vy vz cn wcel cee cfv w3a cop ccgr wbr csegle wi wa cv cbtwn wrex
      ccgr3 simprrl simprlr simpl11 simpl21 simpr simpl22 simpl32 simpl33
      cgrxfr syl132anc adantr mp2and anass wb simprl simprr brcgr3 syl133anc
      df-3an simpl23 simpl31 simpl12 simpl13 simpr1l simpr2r cgrtr4and simpr31
      cgrtrand sylan2br expr sylbid anim2d sylanb an32s reximdva mpd
      r19.23adva simp11 simp12 simp13 simp21 simp22 brsegle syl122anc simp23
      simp31 simp32 simp33 3imtr4d exp32 3impd ) ILMZAINOZMZBWSMZPZCWSMZDWSMZEW
      SMZPZFWSMZGWSMZHWSMZPZPZABQZEFQZRSZCDQZGHQZRSZXLXOTSZXMXPTSZXKXNXQXRXSUAX
      KXNXQUBZUBZJUCZXOUDSZXLCYBQZRSZUBZJWSUEZKUCZXPUDSZXMGYHQZRSZUBZKWSUEZXRXS
      YAYFYMJWSXKYBWSMZXTYFYMUAXKYNUBZXTYFYMYOXTYFUBZUBZYICYBDQZQGYHHQZQUFSZUBZ
      KWSUEZYMYQYCXQUUBYOXTYCYEUGYOXNXQYFUHYOYCXQUBUUBUAZYPYOWRXCYNXDXHXIUUCWRW
      TXAXFXJYNUIXCXDXEXBXJYNUJXKYNUKXCXDXEXBXJYNULXGXHXIXBXFYNUMXGXHXIXBXFYNUN
      CYBDGKHIUOUPUQURYQUUAYLKWSYOYHWSMZYPUUAYLUAZYOUUDUBXKYNUUDUBZUBZYPUUEXKYN
      UUDUSUUGYPUBZYTYKYIUUHYTYDYJRSZXQYRYSRSZPZYKUUGYTUUKUTZYPUUGWRXCYNXDXHUUD
      XIUULWRWTXAXFXJUUFUIZXCXDXEXBXJUUFUJZXKYNUUDVAZXCXDXEXBXJUUFULXGXHXIXBXFU
      UFUMZXKYNUUDVBZXGXHXIXBXFUUFUNCYBDGYHHIVCVDUQUUGYPUUKYKYPUUKUBUUGXTYFUUKP
      ZYKXTYFUUKVEUUGUUREFCYBGYHIUUMXCXDXEXBXJUUFVFZXGXHXIXBXFUUFVGZUUNUUOUUPUU
      QUUGUURABEFCYBIUUMWRWTXAXFXJUUFVHWRWTXAXFXJUUFVIUUSUUTUUNUUOXNXQYFUUKUUGV
      JYCYEXTUUKUUGVKVLUUIXQUUJXTYFUUGVMVNVOVPVQVRVSVTWAWBVPVTWCXKXRYGUTZXTXKWR
      WTXAXCXDUVAWRWTXAXFXJWDZWRWTXAXFXJWEWRWTXAXFXJWFXBXCXDXEXJWGXBXCXDXEXJWHJ
      ABCDIWIWJUQXKXSYMUTZXTXKWRXEXGXHXIUVCUVBXBXCXDXEXJWKXBXFXGXHXIWLXBXFXGXHX
      IWMXBXFXGXHXIWNKEFGHIWIWJUQWOWPWQ $.
      $( [14-Oct-2013] $) $( [12-Oct-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d N y $.
    $( Segment comparison is reflexive.  Theorem 5.7 of [Schwabhauser], p. 42.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    seglerflx $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        <. A , B >. Seg<_ <. A , B >. ) $=
      ( vy cn wcel cee cfv w3a cop csegle wbr cv cbtwn ccgr wa wrex simp3
      btwntriv2 cgrrflx wceq breq1 opeq2 breq2d anbi12d rcla4ev syl12anc wb
      simp1 simp2 brsegle syl122anc mpbird ) CEFZACGHZFZBUOFZIZABJZUSKLZDMZUSNL
      ZUSAVAJZOLZPZDUOQZURUQBUSNLZUSUSOLZVFUNUPUQRZABCSABCTVEVGVHPDBUOVABUAZVBV
      GVDVHVABUSNUBVJVCUSUSOVABAUCUDUEUFUGURUNUPUQUPUQUTVFUHUNUPUQUIUNUPUQUJZVI
      VKVIDABABCUKULUM $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y $.  $d A y $.  $d B y $.  $d C y $.
    $( Any segment is at least as long as a degenerate segment.  Theorem 5.11
       of [Schwabhauser], p. 42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    seglemin $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       <. A , A >. Seg<_ <. B , C >. ) $=
      ( vy cn wcel cee cfv w3a wa cop csegle wbr cv cbtwn ccgr wrex simpr2
      btwntriv1 3adant3r1 cgrtriv 3adant3r3 wceq breq1 opeq2 breq2d anbi12d
      rcla4ev syl12anc wb simpl simpr1 simpr3 brsegle syl122anc mpbird ) DFGZAD
      HIZGZBUSGZCUSGZJZKZAALZBCLZMNZEOZVFPNZVEBVHLZQNZKZEUSRZVDVABVFPNZVEBBLZQN
      ZVMURUTVAVBSZURVAVBVNUTBCDTUAURUTVAVPVBABDUBUCVLVNVPKEBUSVHBUDZVIVNVKVPVH
      BVFPUEVRVJVOVEQVHBBUFUGUHUIUJVDURUTUTVAVBVGVMUKURVCULURUTVAVBUMZVSVQURUTV
      AVBUNEAABCDUOUPUQ $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y z w $.  $d A y z w $.  $d B y z w $.  $d C y z w $.  $d D y z w $.
    $d E y z w $.  $d F y z w $.
    $( Segment less than is transitive.  Theorem 5.8 of [Schwabhauser], p. 42.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    segletr $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. Seg<_ <. C , D >. /\ <. C , D >. Seg<_ <. E , F >. ) ->
         <. A , B >. Seg<_ <. E , F >. ) ) $=
      ( vy vz vw cn wcel cee cfv w3a cv cop cbtwn wbr ccgr wa wrex csegle
      ccgr3 simprll simprrr jca wi simpl1 simpl23 simprl simpl31 simpl32
      simprr cgrxfr syl132anc adantr mpd anass df-3an anbi2i bitr4i wb simpr1
      simpr3 simpr2 brcgr3 syl133anc anbi2d simpl33 simpr3l simpr2l
      btwnexchand simpl21 simpl22 simpr1r simp3r1 adantl cgrtrand sylan2br
      anassrs ex sylbid sylanb an32s reximdva exp31 r19.23advv simp1 simp21
      simp22 simp23 simp31 brsegle syl122anc simp32 simp33 anbi12d reeanv
      syl6bbr 3imtr4d ) GKLZAGMNZLZBXCLZCXCLZOZDXCLZEXCLZFXCLZOZOZHPZCDQZRSZABQ
      ZCXMQZTSZUAZIPZEFQZRSZXNEXTQZTSZUAZUAZIXCUBHXCUBZJPZYARSZXPEYHQZTSZUAZJXC
      UBZXPXNUCSZXNYAUCSZUAZXPYAUCSZXLYFYMHIXCXCXLXMXCLZXTXCLZUAZYFYMXLYTUAZYFU
      AZYHYCRSZCXMDQZQEYHXTQZQUDSZUAZJXCUBZYMUUBXOYDUAZUUHUUBXOYDUUAXOXRYEUEUUA
      XSYBYDUFUGUUAUUIUUHUHZYFUUAXBXFYRXHXIYSUUJXBXGXKYTUIXDXEXFXBXKYTUJXLYRYSU
      KXHXIXJXBXGYTULXHXIXJXBXGYTUMXLYRYSUNCXMDEJXTGUOUPUQURUUBUUGYLJXCUUAYHXCL
      ZYFUUGYLUHZUUAUUKUAZXLYRYSUUKOZUAZYFUULUUMXLYTUUKUAZUAUUOXLYTUUKUSUUNUUPX
      LYRYSUUKUTVAVBUUOYFUAZUUGUUCXQYJTSZYDUUDUUETSZOZUAZYLUUOUUGUVAVCYFUUOUUFU
      UTUUCUUOXBXFYRXHXIUUKYSUUFUUTVCXBXGXKUUNUIZXDXEXFXBXKUUNUJZXLYRYSUUKVDZXH
      XIXJXBXGUUNULXHXIXJXBXGUUNUMZXLYRYSUUKVEZXLYRYSUUKVFZCXMDEYHXTGVGVHVIUQUU
      QUVAYLUUOYFUVAYLYFUVAUAUUOXSYEUVAOZYLXSYEUVAUTUUOUVHUAYIYKUUOUVHEYHXTFGUV
      BUVEUVFUVGXHXIXJXBXGUUNVJUUCUUTXSYEUUOVKYBYDXSUVAUUOVLVMUUOUVHABCXMEYHGUV
      BXDXEXFXBXKUUNVNXDXEXFXBXKUUNVOUVCUVDUVEUVFXOXRYEUVAUUOVPUVHUURUUOUURYDUU
      SUUCXSYEVQVRVSUGVTWAWBWCWDWEWFURWGWHXLYPXSHXCUBZYEIXCUBZUAYGXLYNUVIYOUVJX
      LXBXDXEXFXHYNUVIVCXBXGXKWIZXBXDXEXFXKWJZXBXDXEXFXKWKZXBXDXEXFXKWLZXBXGXHX
      IXJWMZHABCDGWNWOXLXBXFXHXIXJYOUVJVCUVKUVNUVOXBXGXHXIXJWPZXBXGXHXIXJWQZICD
      EFGWNWOWRXSYEHIXCXCWSWTXLXBXDXEXIXJYQYMVCUVKUVLUVMUVPUVQJABEFGWNWOXA $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  ${
    $d N y t $.  $d A y t $.  $d B y t $.  $d C y t $.  $d D y t $.
    $( Antisymmetry law for segment comparison.  Theorem 5.9 of [Schwabhauser],
       p. 42.  (Contributed by Scott Fenton, 14-Oct-2013.) $)
    segleantisym $p |- ( ( N e. NN /\
        ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( ( <. A , B >. Seg<_ <. C , D >. /\ <. C , D >. Seg<_ <. A , B >. ) ->
          <. A , B >. Cgr <. C , D >. ) ) $=
      ( vy vt cn wcel cee cfv wa w3a cop csegle wbr cv cbtwn ccgr wrex brsegle
      wb brsegle2 3com23 anbi12d reeanv syl6bbr weq simpl1 simpl3l simprl
      simpl3r simprr simprll simprrl btwnexchand simpl2l simpl2r simprrr
      simprlr cgrtrand wi endofsegid syl13anc adantr mp2and opeq2 breq2d
      breq1d anbi2d wceq btwncomand btwnswapid syl5ibrcom mpd syl6bi mpcom
      exp31 r19.23advv sylbid ) EHIZAEJKZIZBWBIZLZCWBIZDWBIZLZMZABNZCDNZOPZWKWJ
      OPZLZFQZWKRPZWJCWONZSPZLZDCGQZNZRPZXAWJSPZLZLZGWBTFWBTZWJWKSPZWIWNWSFWBTZ
      XDGWBTZLXFWIWLXHWMXIFABCDEUAWAWHWEWMXIUBGCDABEUCUDUEWSXDFGWBWBUFUGWIXEXGF
      GWBWBWIWOWBIZWTWBIZLZXEXGGFUHZWIXLLZXELZXGXOWOXARPZXAWQSPZXMXNXECWODWTEWA
      WEWHXLUIZWFWGWAWEXLUJZWIXJXKUKZWFWGWAWEXLULZWIXJXKUMZXNWPWRXDUNXNWSXBXCUO
      UPXNXECWTABCWOEXRXSYBWCWDWAWHXLUQWCWDWAWHXLURXSXTXNWSXBXCUSXNWPWRXDUTVAXN
      XPXQLXMVBZXEXNWAWFXJXKYCXRXSXTYBCWOWTEVCVDVEVFXMXOXNWSDWQRPZWQWJSPZLZLZLZ
      XGXMXEYGXNXMXDYFWSXMXBYDXCYEXMXAWQDRWTWOCVGZVHXMXAWQWJSYIVIUEVJVJYHDWOVKZ
      XGYHDWOCNRPZWODCNRPZYJXNYGDCWOEXRYAXSXTXNWSYDYEUOVLXNYGWOCDEXRXTXSYAXNWPW
      RYFUNVLXNYKYLLYJVBZYGXNWAWGXJWFYMXRYAXTXSDWOCEVMVDVEVFYHXGYJWRXNWPWRYFUTY
      JWKWQWJSDWOCVGVHVNVOVPVQVRVSVT $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Linearity law for segment comparison.  Theorem 5.10 of [Schwabhauser],
       p. 42.  (Contributed by Scott Fenton, 14-Oct-2013.) $)
    seglelin $p |- ( ( N e. NN /\
     ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
     ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
     ( <. A , B >. Seg<_ <. C , D >. \/ <. C , D >. Seg<_ <. A , B >. ) ) $=
      ( vx cn wcel cee cfv wa w3a cop csegle wbr wo wceq opeq2 breq1d orbi1d
      wne cv cbtwn ccgr wrex segcon2 imp wb andir simpl1 simp2l anim1i simpl3
      cgrcom syl111anc anbi2d orbi2d syl5bb rexbidva adantr mpbid brsegle2
      brsegle 3com23 orbi12d r19.43 syl6bbr mpbird simp1 simp3l simp3r
      seglemin syl13anc orcd pm2.61ne ) EGHZAEIJZHZBVQHZKZCVQHZDVQHZKZLZABMZCDM
      ZNOZWFWENOZPZAAMZWFNOZWHPBABAQZWGWKWHWLWEWJWFNBAARSTWDBAUAZKZWIBAFUBZMZUC
      OZWPWFUDOZKZWOWEUCOZWFWPUDOZKZPZFVQUEZWNWQWTPWRKZFVQUEZXDWDWMXFFBCDAEUFUG
      WDXFXDUHWMWDXEXCFVQXEWSWTWRKZPWDWOVQHZKZXCWQWTWRUIXIXGXBWSXIWRXAWTXIVPVRX
      HKWCWRXAUHVPVTWCXHUJWDVRXHVPVRVSWCUKZULVPVTWCXHUMAWOCDEUNUOUPUQURUSUTVAWD
      WIXDUHWMWDWIWSFVQUEZXBFVQUEZPXDWDWGXKWHXLFABCDEVBVPWCVTWHXLUHFCDABEVCVDVE
      WSXBFVQVFVGUTVHWDWKWHWDVPVRWAWBWKVPVTWCVIXJVPVTWAWBVJVPVTWAWBVKACDEVLVMVN
      VO $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

$( (End of Scott Fenton's mathbox.) $)


