$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Segment less than or equal to
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Seg<_ $.

  $( Declare the constant for the segment less than or equal to
     relationship. $)
  csegle $a class Seg<_ $.

  ${
    $d p q n a b c d y $.
    $( Define the segment length comparison relationship.  This relationship
       expresses that the segment ` A B ` is no longer than ` C D ` .  In this
       section, we establish various properties of this relationship showing
       that it is a transitive, reflexive relationship on pairs of points that
       is substitutive under congruence.  Definition 5.4 of [Schwabhauser],
       p. 41. $)
    df-segle $a |- Seg<_ = { <. p , q >. |
       E. n e. NN E. a e. ( EE ` n ) E. b e. ( EE ` n ) E. c e. ( EE ` n )
       E. d e. ( EE ` n ) ( p = <. a , b >. /\ q = <. c , d >. /\
         E. y e. ( EE ` n ) ( y Btwn <. c , d >. /\
                              <. a , b >. == <. c , y >. ) ) } $.
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a c $.  $d A c $.  $d a d $.  $d A d $.
    $d a n $.  $d A n $.  $d a p $.  $d A p $.  $d a q $.  $d A q $.  $d a y $.
    $d A y $.  $d B a $.  $d B b $.  $d b c $.  $d B c $.  $d b d $.  $d B d $.
    $d b n $.  $d B n $.  $d b p $.  $d B p $.  $d b q $.  $d B q $.  $d b y $.
    $d B y $.  $d C a $.  $d C b $.  $d C c $.  $d c d $.  $d C d $.  $d c n $.
    $d C n $.  $d c p $.  $d C p $.  $d c q $.  $d C q $.  $d c y $.  $d C y $.
    $d D a $.  $d D b $.  $d D c $.  $d D d $.  $d d n $.  $d D n $.  $d d p $.
    $d D p $.  $d d q $.  $d D q $.  $d d y $.  $d D y $.  $d N a $.  $d N b $.
    $d N c $.  $d N d $.  $d N n $.  $d n p $.  $d n q $.  $d n y $.  $d N y $.
    $d p q $.  $d p y $.  $d q y $.
    $( Binary relationship form of the segment comparison relationship.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    brsegle $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( <. A , B >. Seg<_ <. C , D >. <->
          E. y e. ( EE ` N ) ( y Btwn <. C , D >. /\
                  <. A , B >. == <. C , y >. ) ) ) $=
      ( va vb vc vd vn cop wbr cv wceq wa wrex w3a cn wcel vp csegle cbtwn ccgr
      cee cfv opex eqeq1 eqcom syl6bb 3anbi1d rexbidv 2rexbidv 3anbi2d df-segle
      vq brab wb simp2r visset opthg simp3r 3anbi12d wi simpl2l ad2antrl eleenn
      syl simprlr simprll adantl axdimuniq syl22anc fveq2d rexeqdv exbiri eleq1
      anassrs bi2anan9 anbi2d opeq12 breq1d breq2d opeq1 adantr sylan9bb imbi1d
      anbi12d 3imtr4d com12 exp3a 3impd expr rexlimdvv rexlimdva sylbid simpl2r
      simpl1 simpl3l simpl3r eqidd simpr eqeq1d 3anbi23d opeq2 anbi1d syl113anc
      rcla42ev 3anbi13d syl111anc fveq2 3anbi3d rexeqbidv rcla4ev impbid syl5bb
      ex syl11anc ) BCLZDELZUBMGNZHNZLZXSOZINZJNZLZXTOZANZYGUCMZYCYEYILZUDMZPZA
      KNZUEUFZQZRZJYOQZIYOQZHYOQZGYOQZKSQZFSTZBFUEUFZTZCUUDTZPZDUUDTZEUUDTZPZRZ
      YIXTUCMZXSDYILZUDMZPZAUUDQZUANZYCOZUPNZYGOZYPRZJYOQZIYOQHYOQZGYOQKSQYDUUT
      YPRZJYOQZIYOQHYOQZGYOQKSQUUBUAUPXSXTUBBCUGDEUGUUQXSOZUVCUVFKGSYOUVGUVBUVE
      HIYOYOUVGUVAUVDJYOUVGUURYDUUTYPUVGUURXSYCOYDUUQXSYCUHXSYCUIUJUKULUMUMUUSX
      TOZUVFYTKGSYOUVHUVEYRHIYOYOUVHUVDYQJYOUVHUUTYHYDYPUVHUUTXTYGOYHUUSXTYGUHX
      TYGUIUJUNULUMUMAKUPUAGHIJUOUQUUKUUBUUPUUKUUBYABOZYBCOZPZYEDOZYFEOZPZYPRZJ
      YOQIYOQZHYOQGYOQZKSQUUPUUKUUAUVQKSUUKYSUVPGHYOYOUUKYQUVOIJYOYOUUKYDUVKYHU
      VNYPUUKUUFYDUVKURUUCUUEUUFUUJUSYAYBBCUUDGUTHUTVAVHUUKUUIYHUVNURUUCUUGUUHU
      UIVBYEYFDEUUDIUTJUTVAVHVCUMUMULUUKUVQUUPKSUUKYNSTZPZUVPUUPGHYOYOUVSYAYOTZ
      YBYOTZPZUVPUUPVDUVSUWBPUVOUUPIJYOYOUVSUWBYEYOTZYFYOTZPZUVOUUPVDUVSUWBUWEP
      ZPZUVKUVNYPUUPUWGUVKUVNYPUUPVDZUVKUVNPZUWGUWHUWIUVSBYOTZCYOTZPZDYOTZEYOTZ
      PZPZPZUUOAYOQZUUPVDZUWGUWHUVIUVJUVNUWQUWSVDUVIUVJUVNPPZUWQUUPUWRUWTUWQPZU
      UOAUUDYOUXAFYNUEUXAUUCUUEUVRUWJFYNOUXAUUEUUCUVSUUEUWTUWPUUEUUFUUCUUJUVRVE
      VFZBFVGVHUXBUWTUUKUVRUWPVIUWQUWJUWTUVSUWJUWKUWOVJVKBYNFVLVMVNVOVPVRUWIUWF
      UWPUVSUVKUWBUWLUVNUWEUWOUVIUVTUWJUVJUWAUWKYABYOVQYBCYOVQVSUVLUWCUWMUVMUWD
      UWNYEDYOVQYFEYOVQVSVSVTUWIYPUWRUUPUWIYMUUOAYOUVKYMYJXSYKUDMZPZUVNUUOUVKYL
      UXCYJUVKYCXSYKUDYAYBBCWAWBVTUVNYJUULUXCUUNUVNYGXTYIUCYEYFDEWAWCUVLUXCUUNU
      RUVMUVLYKUUMXSUDYEDYIWDWCZWEWHWFULWGWIWJWKWLWMWNXQWNWOWPUUKUUPUUBUUKUUPPZ
      UUCYDYHYMAUUDQZRZJUUDQZIUUDQZHUUDQZGUUDQZUUBUUCUUGUUJUUPWRUXFUUEUUFXSXSOZ
      YHUXDAUUDQZRZJUUDQIUUDQZUXLUUEUUFUUCUUJUUPVEUUEUUFUUCUUJUUPWQUXFUUHUUIUXM
      XTXTOZUUPUXPUUHUUIUUCUUGUUPWSUUHUUIUUCUUGUUPWTUXFXSXAUXFXTXAUUKUUPXBUXOUX
      MUXQUUPRUXMDYFLZXTOZYIUXRUCMZUUNPZAUUDQZRIJDEUUDUUDUVLYHUXSUXNUYBUXMUVLYG
      UXRXTYEDYFWDZXCUVLUXDUYAAUUDUVLYJUXTUXCUUNUVLYGUXRYIUCUYCWCUXEWHULXDUVMUX
      SUXQUYBUUPUXMUVMUXRXTXTYFEDXEZXCUVMUYAUUOAUUDUVMUXTUULUUNUVMUXRXTYIUCUYDW
      CXFULXDXHXGUXJUXPBYBLZXSOZYHYJUYEYKUDMZPZAUUDQZRZJUUDQIUUDQGHBCUUDUUDUVIU
      XHUYJIJUUDUUDUVIYDUYFUXGUYIYHUVIYCUYEXSYABYBWDZXCUVIYMUYHAUUDUVIYLUYGYJUV
      IYCUYEYKUDUYKWBVTULXIUMUVJUYJUXOIJUUDUUDUVJUYFUXMUYIUXNYHUVJUYEXSXSYBCBXE
      ZXCUVJUYHUXDAUUDUVJUYGUXCYJUVJUYEXSYKUDUYLWBVTULXIUMXHXJUUAUXLKFSYNFOZYTU
      XKGYOUUDYNFUEXKZUYMYSUXJHYOUUDUYNUYMYRUXIIYOUUDUYNUYMYQUXHJYOUUDUYNUYMYPU
      XGYDYHUYMYMAYOUUDUYNVOXLXMXMXMXMXNXRXQXOXP $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N x y $.  $d A x y $.  $d B x y $.  $d C x y $.  $d D x y $.
    $( Alternate characterization of segment comparison.  Theorem 5.5 of
       [Schwabhauser], p. 41-42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    brsegle2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( <. A , B >. Seg<_ <. C , D >. <->
          E. x e. ( EE ` N ) ( B Btwn <. A , x >. /\
                  <. A , x >. == <. C , D >. ) ) ) $=
      ( vy wcel wa w3a cop wbr ccgr wrex wi adantr mpd mp2and wb 3ad2ant1 cn cv
      cee csegle cbtwn brsegle ccgr3 ccolin simprl simpl1 simpl3l simpl3r simpr
      cfv btwncolinear1 colinearperm1 sylibd syl13anc simpl2l simpl2r cgrcomand
      simprr simpl2 lineext syl131anc an32 simplr brcgr3 syl133anc simp2l simp3
      simpll1 mpbird btwnxfr simp32 cgrcom syl122anc mpbid 3expia sylbid sylanb
      jca an32s reximdva rexlimdva simpl3 3jca syl113anc simp2r simp33 cgrcomlr
      ex impbid bitrd ) FUAHZBFUCUNZHZCWPHZIZDWPHZEWPHZIZJZBCKZDEKZUDLGUBZXEUEL
      ZXDDXFKZMLZIZGWPNZCBAUBZKZUELZXMXEMLZIZAWPNZGBCDEFUFXCXKXQXCXJXQGWPXCXFWP
      HZIZXJXQXSXJIZDXFEKZKZBCXLKZKZUGLZAWPNZXQXTDYAUHLZXHXDMLZYFXTXGYGXSXGXIUI
      XSXGYGOZXJXSWOWTXAXRYIWOWSXBXRUJZWTXAWOWSXRUKZWTXAWOWSXRULZXCXRUMZWOWTXAX
      RJIXGDEXFKZUHLYGDEXFFUODEXFFUPUQURPQXSXJBCDXFFYJWQWRWOXBXRUSWQWRWOXBXRUTY
      KYMXSXGXIVBVAXSYGYHIYFOZXJXSWOWTXRXAWSYOYJYKYMYLWOWSXBXRVCDXFEBACFVDVEPRX
      TYEXPAWPXSXLWPHZXJYEXPOZXSYPIXCYPIZXRIZXJYQXCXRYPVFYSXJIYEYHXEXMMLZYAYCML
      ZJZXPYSYEUUBSZXJYSWOWTXRXAWQWRYPUUCWOWSXBYPXRVLZYRWTXRWTXAWOWSYPUKPZYRXRU
      MZYRXAXRWTXAWOWSYPULPZYRWQXRWQWRWOXBYPUSZPZYRWRXRWQWRWOXBYPUTZPZXCYPXRVGZ
      DXFEBCXLFVHVIZPYSXJUUBXPYSXJUUBJZXNXOUUNXGYEXNYSXGXIUUBVJUUNYEUUBYSXJUUBV
      KYSXJUUCUUBUUMTVMYSXJXGYEIXNOZUUBYSWOWTXRXAWQWRYPUUOUUDUUEUUFUUGUUIUUKUUL
      DXFEBCXLFVNVITRUUNYTXOYSXJYHYTUUAVOYSXJYTXOSZUUBYSWOWTXAWQYPUUPUUDUUEUUGU
      UIUULDEBXLFVPVQTVRWBVSVTWAWCWDQWLWEXCXPXKAWPYRXPXKYRXPIZBXLCKZKDYNKUGLZGW
      PNZXKUUQBUURUHLZXOUUTUUQXNUVAYRXNXOUIUUQWOWQYPWRXNUVAOWOWSXBYPXPVLYRWQXPU
      UHPXCYPXPVGYRWRXPUUJPBXLCFUOURQYRXNXOVBYRUVAXOIUUTOZXPYRWOWQYPWRXBUVBWOWS
      XBYPUJUUHXCYPUMZUUJWOWSXBYPWFBXLCDGEFVDVEPRUUQUUSXJGWPYRXRXPUUSXJOYSXPIUU
      SXOXIUURYNMLZJZXJYSUUSUVESZXPYSWOWQYPWRJZWTXAXRUVFUUDYRUVGXRYRWQYPWRUUHUV
      CUUJWGPUUEUUGUUFBXLCDEXFFVHWHPYSXPUVEXJYSXPUVEJZXGXIUVHXNYDYBUGLZXGYSXNXO
      UVEVJUVHUVIXIXOYCYAMLZJZUVHXIXOUVJYSXPXOXIUVDVOZYSXNXOUVEWIUVHUVDUVJYSXPX
      OXIUVDWJYSXPUVDUVJSZUVEYSWOYPWRXAXRUVMUUDUULUUKUUGUUFXLCEXFFWKVQTVRWGYSXP
      UVIUVKSZUVEYSWOWQWRYPWTXRXAUVNUUDUUIUUKUULUUEUUFUUGBCXLDXFEFVHVITVMYSXPXN
      UVIIXGOZUVEYSWOWQWRYPWTXRXAUVOUUDUUIUUKUULUUEUUFUUGBCXLDXFEFVNVITRUVLWBVS
      VTWCWDQWLWEWMWN $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y z $.  $d A y z $.  $d B y z $.  $d C y z $.  $d D y z $.
    $d E y z $.  $d F y z $.  $d G y z $.  $d H y z $.
    $( Substitution law for segment comparison under congruence.  Theorem 5.6
       of [Schwabhauser], p. 42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    seglecgr12 $p |- (
       ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ E e. ( EE ` N ) ) /\
         ( F e. ( EE ` N ) /\ G e. ( EE ` N ) /\ H e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. == <. E , F >. /\ <. C , D >. == <. G , H >. /\
           <. A , B >. Seg<_ <. C , D >. ) ->
           <. E , F >. Seg<_ <. G , H >. ) ) $=
      ( vy vz wcel w3a cop ccgr wbr wi wa wrex adantr cn cee csegle cbtwn ccgr3
      cv simprrl simprlr simpl11 simpl21 simpr simpl22 simpl32 cgrxfr syl132anc
      cfv simpl33 mp2and anass wb simprl simprr brcgr3 syl133anc df-3an simpl23
      simpl31 simpl12 simpl13 simpr1l simpr2r cgrtr4and simpr31 cgrtrand sylbid
      sylan2br expr anim2d sylanb an32s reximdva rexlimdva simp11 simp12 simp13
      simp21 simp22 brsegle syl122anc simp23 simp31 simp32 simp33 3imtr4d exp32
      mpd 3impd ) IUALZAIUBUPZLZBWSLZMZCWSLZDWSLZEWSLZMZFWSLZGWSLZHWSLZMZMZABNZ
      EFNZOPZCDNZGHNZOPZXLXOUCPZXMXPUCPZXKXNXQXRXSQXKXNXQRZRZJUFZXOUDPZXLCYBNZO
      PZRZJWSSZKUFZXPUDPZXMGYHNZOPZRZKWSSZXRXSYAYFYMJWSXKYBWSLZXTYFYMQXKYNRZXTY
      FYMYOXTYFRZRZYICYBDNZNGYHHNZNUEPZRZKWSSZYMYQYCXQUUBYOXTYCYEUGYOXNXQYFUHYO
      YCXQRUUBQZYPYOWRXCYNXDXHXIUUCWRWTXAXFXJYNUIXCXDXEXBXJYNUJXKYNUKXCXDXEXBXJ
      YNULXGXHXIXBXFYNUMXGXHXIXBXFYNUQCYBDGKHIUNUOTURYQUUAYLKWSYOYHWSLZYPUUAYLQ
      ZYOUUDRXKYNUUDRZRZYPUUEXKYNUUDUSUUGYPRZYTYKYIUUHYTYDYJOPZXQYRYSOPZMZYKUUG
      YTUUKUTZYPUUGWRXCYNXDXHUUDXIUULWRWTXAXFXJUUFUIZXCXDXEXBXJUUFUJZXKYNUUDVAZ
      XCXDXEXBXJUUFULXGXHXIXBXFUUFUMZXKYNUUDVBZXGXHXIXBXFUUFUQCYBDGYHHIVCVDTUUG
      YPUUKYKYPUUKRUUGXTYFUUKMZYKXTYFUUKVEUUGUUREFCYBGYHIUUMXCXDXEXBXJUUFVFZXGX
      HXIXBXFUUFVGZUUNUUOUUPUUQUUGUURABEFCYBIUUMWRWTXAXFXJUUFVHWRWTXAXFXJUUFVIU
      USUUTUUNUUOXNXQYFUUKUUGVJYCYEXTUUKUUGVKVLUUIXQUUJXTYFUUGVMVNVPVQVOVRVSVTW
      AWPVQVTWBXKXRYGUTZXTXKWRWTXAXCXDUVAWRWTXAXFXJWCZWRWTXAXFXJWDWRWTXAXFXJWEX
      BXCXDXEXJWFXBXCXDXEXJWGJABCDIWHWITXKXSYMUTZXTXKWRXEXGXHXIUVCUVBXBXCXDXEXJ
      WJXBXFXGXHXIWKXBXFXGXHXIWLXBXFXGXHXIWMKEFGHIWHWITWNWOWQ $.
      $( [14-Oct-2013] $) $( [12-Oct-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d N y $.
    $( Segment comparison is reflexive.  Theorem 5.7 of [Schwabhauser], p. 42.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    seglerflx $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
        <. A , B >. Seg<_ <. A , B >. ) $=
      ( vy cn wcel cee cfv w3a cop csegle cv cbtwn ccgr wa wrex simp3 btwntriv2
      wbr cgrrflx wceq breq1 opeq2 anbi12d rcla4ev syl12anc simp1 simp2 brsegle
      breq2d wb syl122anc mpbird ) CEFZACGHZFZBUOFZIZABJZUSKSZDLZUSMSZUSAVAJZNS
      ZOZDUOPZURUQBUSMSZUSUSNSZVFUNUPUQQZABCRABCTVEVGVHODBUOVABUAZVBVGVDVHVABUS
      MUBVJVCUSUSNVABAUCUJUDUEUFURUNUPUQUPUQUTVFUKUNUPUQUGUNUPUQUHZVIVKVIDABABC
      UIULUM $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y $.  $d A y $.  $d B y $.  $d C y $.
    $( Any segment is at least as long as a degenerate segment.  Theorem 5.11
       of [Schwabhauser], p. 42.  (Contributed by Scott Fenton,
       11-Oct-2013.) $)
    seglemin $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       <. A , A >. Seg<_ <. B , C >. ) $=
      ( vy cn wcel cee cfv w3a wa cop csegle wbr cv cbtwn ccgr simpr2 btwntriv1
      wrex 3adant3r1 cgrtriv 3adant3r3 wceq breq1 opeq2 breq2d anbi12d syl12anc
      rcla4ev wb simpl simpr1 simpr3 brsegle syl122anc mpbird ) DFGZADHIZGZBUSG
      ZCUSGZJZKZAALZBCLZMNZEOZVFPNZVEBVHLZQNZKZEUSTZVDVABVFPNZVEBBLZQNZVMURUTVA
      VBRZURVAVBVNUTBCDSUAURUTVAVPVBABDUBUCVLVNVPKEBUSVHBUDZVIVNVKVPVHBVFPUEVRV
      JVOVEQVHBBUFUGUHUJUIVDURUTUTVAVBVGVMUKURVCULURUTVAVBUMZVSVQURUTVAVBUNEAAB
      CDUOUPUQ $.
      $( [14-Oct-2013] $) $( [11-Oct-2013] $)
  $}

  ${
    $d N y z w $.  $d A y z w $.  $d B y z w $.  $d C y z w $.  $d D y z w $.
    $d E y z w $.  $d F y z w $.
    $( Segment less than is transitive.  Theorem 5.8 of [Schwabhauser], p. 42.
       (Contributed by Scott Fenton, 11-Oct-2013.) $)
    segletr $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
       ( ( <. A , B >. Seg<_ <. C , D >. /\ <. C , D >. Seg<_ <. E , F >. ) ->
         <. A , B >. Seg<_ <. E , F >. ) ) $=
      ( vy vz vw wcel w3a cv cop cbtwn wbr ccgr wa wrex wb cee cfv csegle ccgr3
      cn simprll simprrr wi simpl1 simpl23 simprl simpl31 simpl32 simprr cgrxfr
      jca syl132anc adantr mpd df-3an anbi2i bitr4i simpr1 simpr3 simpr2 brcgr3
      anass simpl33 simpr3l simpr2l btwnexchand simpl21 simpl22 simpr1r simp3r1
      syl133anc anbi2d adantl cgrtrand sylan2br ex sylbid sylanb an32s reximdva
      anassrs exp31 rexlimdvv simp21 simp22 simp23 simp31 brsegle simp32 simp33
      simp1 syl122anc anbi12d reeanv syl6bbr 3imtr4d ) GUEKZAGUAUBZKZBXCKZCXCKZ
      LZDXCKZEXCKZFXCKZLZLZHMZCDNZOPZABNZCXMNZQPZRZIMZEFNZOPZXNEXTNZQPZRZRZIXCS
      HXCSZJMZYAOPZXPEYHNZQPZRZJXCSZXPXNUCPZXNYAUCPZRZXPYAUCPZXLYFYMHIXCXCXLXMX
      CKZXTXCKZRZYFYMXLYTRZYFRZYHYCOPZCXMDNZNEYHXTNZNUDPZRZJXCSZYMUUBXOYDRZUUHU
      UBXOYDUUAXOXRYEUFUUAXSYBYDUGUPUUAUUIUUHUHZYFUUAXBXFYRXHXIYSUUJXBXGXKYTUIX
      DXEXFXBXKYTUJXLYRYSUKXHXIXJXBXGYTULXHXIXJXBXGYTUMXLYRYSUNCXMDEJXTGUOUQURU
      SUUBUUGYLJXCUUAYHXCKZYFUUGYLUHZUUAUUKRZXLYRYSUUKLZRZYFUULUUMXLYTUUKRZRUUO
      XLYTUUKVGUUNUUPXLYRYSUUKUTVAVBUUOYFRZUUGUUCXQYJQPZYDUUDUUEQPZLZRZYLUUOUUG
      UVATYFUUOUUFUUTUUCUUOXBXFYRXHXIUUKYSUUFUUTTXBXGXKUUNUIZXDXEXFXBXKUUNUJZXL
      YRYSUUKVCZXHXIXJXBXGUUNULXHXIXJXBXGUUNUMZXLYRYSUUKVDZXLYRYSUUKVEZCXMDEYHX
      TGVFVPVQURUUQUVAYLUUOYFUVAYLYFUVARUUOXSYEUVALZYLXSYEUVAUTUUOUVHRYIYKUUOUV
      HEYHXTFGUVBUVEUVFUVGXHXIXJXBXGUUNVHUUCUUTXSYEUUOVIYBYDXSUVAUUOVJVKUUOUVHA
      BCXMEYHGUVBXDXEXFXBXKUUNVLXDXEXFXBXKUUNVMUVCUVDUVEUVFXOXRYEUVAUUOVNUVHUUR
      UUOUURYDUUSUUCXSYEVOVRVSUPVTWFWAWBWCWDWEUSWGWHXLYPXSHXCSZYEIXCSZRYGXLYNUV
      IYOUVJXLXBXDXEXFXHYNUVITXBXGXKWPZXBXDXEXFXKWIZXBXDXEXFXKWJZXBXDXEXFXKWKZX
      BXGXHXIXJWLZHABCDGWMWQXLXBXFXHXIXJYOUVJTUVKUVNUVOXBXGXHXIXJWNZXBXGXHXIXJW
      OZICDEFGWMWQWRXSYEHIXCXCWSWTXLXBXDXEXIXJYQYMTUVKUVLUVMUVPUVQJABEFGWMWQXA
      $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}

  ${
    $d N y t $.  $d A y t $.  $d B y t $.  $d C y t $.  $d D y t $.
    $( Antisymmetry law for segment comparison.  Theorem 5.9 of [Schwabhauser],
       p. 42.  (Contributed by Scott Fenton, 14-Oct-2013.) $)
    segleantisym $p |- ( ( N e. NN /\
        ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
        ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
        ( ( <. A , B >. Seg<_ <. C , D >. /\ <. C , D >. Seg<_ <. A , B >. ) ->
          <. A , B >. == <. C , D >. ) ) $=
      ( vy vt wcel wa cop csegle wbr cv cbtwn ccgr wrex anbi12d simprll simprrl
      simprlr cn cee cfv w3a brsegle brsegle2 3com23 reeanv syl6bbr weq simpl3l
      simpl1 simprl simpl3r simprr btwnexchand simpl2l simpl2r simprrr cgrtrand
      wb endofsegid syl13anc adantr mp2and breq2d breq1d anbi2d wceq btwncomand
      wi opeq2 btwnswapid syl5ibrcom mpd syl6bi mpcom exp31 rexlimdvv sylbid )
      EUAHZAEUBUCZHZBWBHZIZCWBHZDWBHZIZUDZABJZCDJZKLZWKWJKLZIZFMZWKNLZWJCWOJZOL
      ZIZDCGMZJZNLZXAWJOLZIZIZGWBPFWBPZWJWKOLZWIWNWSFWBPZXDGWBPZIXFWIWLXHWMXIFA
      BCDEUEWAWHWEWMXIVAGCDABEUFUGQWSXDFGWBWBUHUIWIXEXGFGWBWBWIWOWBHZWTWBHZIZXE
      XGGFUJZWIXLIZXEIZXGXOWOXANLZXAWQOLZXMXNXECWODWTEWAWEWHXLULZWFWGWAWEXLUKZW
      IXJXKUMZWFWGWAWEXLUNZWIXJXKUOZXNWPWRXDRXNWSXBXCSUPXNXECWTABCWOEXRXSYBWCWD
      WAWHXLUQWCWDWAWHXLURXSXTXNWSXBXCUSXNWPWRXDTUTXNXPXQIXMVKZXEXNWAWFXJXKYCXR
      XSXTYBCWOWTEVBVCVDVEXMXOXNWSDWQNLZWQWJOLZIZIZIZXGXMXEYGXNXMXDYFWSXMXBYDXC
      YEXMXAWQDNWTWOCVLZVFXMXAWQWJOYIVGQVHVHYHDWOVIZXGYHDWOCJNLZWODCJNLZYJXNYGD
      CWOEXRYAXSXTXNWSYDYESVJXNYGWOCDEXRXTXSYAXNWPWRYFRVJXNYKYLIYJVKZYGXNWAWGXJ
      WFYMXRYAXTXSDWOCEVMVCVDVEYHXGYJWRXNWPWRYFTYJWKWQWJODWOCVLVFVNVOVPVQVRVSVT
      $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Linearity law for segment comparison.  Theorem 5.10 of [Schwabhauser],
       p. 42.  (Contributed by Scott Fenton, 14-Oct-2013.) $)
    seglelin $p |- ( ( N e. NN /\
     ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
     ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
     ( <. A , B >. Seg<_ <. C , D >. \/ <. C , D >. Seg<_ <. A , B >. ) ) $=
      ( vx cn wcel cee cfv wa cop csegle wbr wo cbtwn ccgr wrex wb adantr opeq2
      w3a wceq breq1d orbi1d wne segcon2 imp simpl1 simp2l anim1i simpl3 cgrcom
      cv andir syl111anc anbi2d orbi2d syl5bb rexbidva brsegle2 brsegle orbi12d
      mpbid 3com23 r19.43 mpbird simp1 simp3l simp3r seglemin syl13anc pm2.61ne
      syl6bbr orcd ) EGHZAEIJZHZBVQHZKZCVQHZDVQHZKZUBZABLZCDLZMNZWFWEMNZOZAALZW
      FMNZWHOBABAUCZWGWKWHWLWEWJWFMBAAUAUDUEWDBAUFZKZWIBAFUNZLZPNZWPWFQNZKZWOWE
      PNZWFWPQNZKZOZFVQRZWNWQWTOWRKZFVQRZXDWDWMXFFBCDAEUGUHWDXFXDSWMWDXEXCFVQXE
      WSWTWRKZOWDWOVQHZKZXCWQWTWRUOXIXGXBWSXIWRXAWTXIVPVRXHKWCWRXASVPVTWCXHUIWD
      VRXHVPVRVSWCUJZUKVPVTWCXHULAWOCDEUMUPUQURUSUTTVDWDWIXDSWMWDWIWSFVQRZXBFVQ
      RZOXDWDWGXKWHXLFABCDEVAVPWCVTWHXLSFCDABEVBVEVCWSXBFVQVFVNTVGWDWKWHWDVPVRW
      AWBWKVPVTWCVHXJVPVTWAWBVIVPVTWAWBVJACDEVKVLVOVM $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

$( (End of Scott Fenton's mathbox.) $)


