$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Pythagorean Triples
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A n m k $.  $d B n m k $.  $d C n m k $.
    $( Lemma for ~ pythagtrip .  Prove a weaker version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem1 $p |- (
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) $=
      ( c2 cexp co cmin cmul wceq caddc cn wcel cc sqcl syl 2cn c4 cv w3a wa wi
      wrex nncn adantl axmulcl syl2anc mulcl subcl syl11anc adantr ancoms add32
      sylancr syl111anc subadd23 sqmul sq2 a1i opreq12d eqtrd opreq1d 4re recni
      2p2e4 subaddrii opreq1i subdir mp3an12 syl5reqr opreq2d binom2sub 3eqtr4d
      binom2 3adant3 simp3 3ad2ant2 3ad2ant1 3ad2ant3 adddi eqtr4d addcl syl3an
      opreq1 opreqan12d eqeq12d syl5ibrcom 3expa rexlimdva rexlimivv ) ADUAZEUA
      ZGHIZFUAZGHIZJIZKIZLZBWMGWNWPKIZKIZKIZLZCWMWOWQMIZKIZLZUBZDNUEAGHIZBGHIZM
      IZCGHIZLZFENNWPNOZWNNOZUCXHXMDNXNXOWMNOZXHXMUDXNXOXPUBXMXHWSGHIZXCGHIZMIZ
      XFGHIZLZXNWPPOZXOWNPOZXPWMPOZYAWPUFWNUFWMUFYBYCYDUBZWMGHIZWRGHIZXBGHIZMIZ
      KIZYFXEGHIZKIZXSXTYEYIYKYFKYBYCYIYKLYDYBYCUCZWOGHIZGWOWQKIZKIZJIZWQGHIZMI
      ZYHMIZYNYPMIZYRMIZYIYKYMYTYQYHMIZYRMIZUUBYMYQPOZYRPOZYHPOZYTUUDLYMYNPOZYP
      POZUUEYMWOPOZUUHYCUUJYBWNQZUGWOQRZYMGPOZYOPOZUUISYCUUJWQPOZUUNYBUUKWPQZWO
      WQUHUIZGYOUJUPZYNYPUKULYMUUOUUFYBUUOYCUUPUMWQQRYMXBPOZUUGYMUUMXAPOZUUSSYC
      YBUUTWNWPUHUNZGXAUJZUPXBQZRZYQYRYHUOUQYMUUCUUAYRMYMUUCYNYHYPJIZMIZUUAYMUU
      HUUIUUGUUCUVFLUULUURUVDYNYPYHURUQYMUVEYPYNMYMUVETYOKIZYPJIZYPYMYHUVGYPJYM
      YHGGHIZXAGHIZKIZUVGYMUUMUUTYHUVKLSUVAGXAUSUPYMUVITUVJYOKUVITLYMUTVAYCYBUV
      JYOLWNWPUSUNVBVCVDYMYPTGJIZYOKIZUVHUVLGYOKTGGTVEVFZSSVGVHVIYMUUNUVMUVHLZU
      UQTPOUUMUUNUVOUVNSTGYOVJVKRVLVCVMVCVDVCYMYGYSYHMYCUUJUUOYGYSLYBUUKUUPWOWQ
      VNUIVDYCUUJUUOYKUUBLYBUUKUUPWOWQVPUIVOVQVMYEXSYFYGKIZYFYHKIZMIZYJYEXQUVPX
      RUVQMYEYDWRPOZXQUVPLYBYCYDVRZYEUUJUUOUVSYCYBUUJYDUUKVSZYBYCUUOYDUUPVTZWOW
      QUKULZWMWRUSULYEYDUUSXRUVQLUVTYEUUMUUTUUSSYBYCUUTYDUVAVQUVBUPZWMXBUSULVBY
      EYFPOZYGPOZUUGYJUVRLYDYBUWEYCWMQWAYEUVSUWFUWCWRQRYEUUSUUGUWDUVCRYFYGYHWBU
      QWCYEYDXEPOZXTYLLUVTYEUUJUUOUWGUWAUWBWOWQWDULWMXEUSULVOWEXHXKXSXLXTWTXDXK
      XSLXGWTXDXIXQXJXRMAWSGHWFBXCGHWFWGVQXGWTXLXTLXDCXFGHWFWAWHWIWJWKWL $.
      $( [19-Apr-2014] $) $( [28-Mar-2014] $)

    $( Lemma for ~ pythagtrip .  Prove the full version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem2 $p |- ( ( A e. NN /\ B e. NN ) -> (
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) ) $=
      ( cn wcel wa cv c2 cexp co cmul wceq caddc wrex wo cvv cc cpr cmin w3a wb
      oprex preq12bg mpanr12 anbi1d andir df-3an orbi12i bitr4i syl6bb 2rexbidv
      rexbidv r19.43 2rexbii rexbii 3bitri wi pythagtriplem1 3ancoma sylbi nncn
      a1i sqcl syl addcom syl2an eqeq1d syl5ibr jaod sylbid ) AGHZBGHZIZABUADJZ
      EJZKLMZFJZKLMZUBMZNMZVQKVRVTNMNMZNMZUAOZCVQVSWAPMNMOZIZDGQZEGQFGQZAWCOZBW
      EOZWGUCZDGQZEGQZFGQZAWEOZBWCOZWGUCZDGQZEGQZFGQZRZAKLMZBKLMZPMZCKLMZOZVPWJ
      WMWSRZDGQZEGQFGQZXCVPWIXJFEGGVPWHXIDGVPWHWKWLIZWQWRIZRZWGIZXIVPWFXNWGVPWC
      SHWESHWFXNUDVQWBNUEVQWDNUEABWCWEGGSSUFUGUHXOXLWGIZXMWGIZRXIXLXMWGUIWMXPWS
      XQWKWLWGUJWQWRWGUJUKULUMUOUNXKWNWTRZEGQZFGQWOXARZFGQXCXJXRFEGGWMWSDGUPUQX
      SXTFGWNWTEGUPURWOXAFGUPUSUMVPWPXHXBWPXHUTVPABCDEFVAVEXBXHVPXEXDPMZXGOZXBW
      RWQWGUCZDGQZEGQFGQYBWTYDFEGGWSYCDGWQWRWGVBURUQBACDEFVAVCVPXFYAXGVNXDTHZXE
      THZXFYAOVOVNATHYEAVDAVFVGVOBTHYFBVDBVFVGXDXEVHVIVJVKVLVM $.
      $( [19-Apr-2014] $) $( [29-Mar-2014] $)

  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C ` and ` B ` are relatively prime
       under some conditions.  (Contributed by Scott Fenton, 8-Apr-2014.) $)
    pythagtriplem3 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( B gcd C ) = 1 ) $=
      ( cn wcel w3a c2 cexp co wceq cgcd c1 wbr wa cz nnz syl 3ad2ant1 syl11anc
      cc0 caddc cdivides opreq2 adantl zsqcl gcdadd gcdcom eqtr3d adantr simpl2
      3ad2ant2 simpl3 sqgcd simpl1 3eqtr4d 3adant3 simp3l opreq1d eqtrd cle cn0
      wn cr 3ad2ant3 gcdcl nn0re nn0ge0 1re 0re lt01 ltleii sq11 mpanr12 mpbid
      wb ) ADEZBDEZCDEZFZAGHIZBGHIZUAIZCGHIZJZABKIZLJZGAUBMVBZNZFZBCKIZGHIZLGHI
      ZJZWJLJZWIWKWEGHIZWLVSWDWKWOJWHVSWDNZWAWCKIZVTWAKIZWKWOWPWAWBKIZWQWRWDWSW
      QJVSWBWCWAKUCUDVSWSWRJWDVSWAVTKIZWSWRVSWAOEZVTOEZWTWSJVQVPXAVRVQBOEZXABPZ
      BUEQUKZVPVQXBVRVPAOEXBAPAUEQRZWAVTUFSVSXAXBWTWRJXEXFWAVTUGSUHUIUHWPVQVRWK
      WQJVPVQVRWDUJZVPVQVRWDULBCUMSWPVPVQWOWRJVPVQVRWDUNXGABUMSUOUPWIWELGHVSWDW
      FWGUQURUSWIWJVCEZTWJUTMZWMWNVOZVSWDXHWHVSWJVAEZXHVSXCCOEZXKVQVPXCVRXDUKVR
      VPXLVQCPVDBCVESZWJVFQRVSWDXIWHVSXKXIXMWJVGQRXHXINLVCETLUTMXJVHTLVIVHVJVKW
      JLVLVMSVN $.
      $( [19-Apr-2014] $) $( [8-Apr-2014] $)
  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C - B ` and ` C + B ` are
       relatively prime.  (Contributed by Scott Fenton, 12-Apr-2014.) $)
    pythagtriplem4 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( ( C - B ) gcd ( C + B ) ) = 1 ) $=
      ( cn wcel c2 co caddc wceq cgcd c1 cdivides wbr wa cmin cmul syl11anc syl
      cz cc w3a cexp wn wo simp3r zsubcl syl2anc 3adant1 3ad2ant1 simp13 simp12
      nnz nnaddcl gcddvds simprd breq1 biimpd simpl13 simpl12 zaddcl dvdsmultr1
      mpan9 wi 2z mp3an1 mpd subsq breqtrrd simpl2 opreq1d simpl11 nnsqcl pncan
      nncn eqtr3d breqtrd adantr cprime 2prm 2nn prmdvdsexp mp3an13 mpbid mtand
      wb cneg 1nn0 nn0negzi gcdaddm pnncan 3anidm23 subcl mulm1 opreq2d axaddcl
      negsub eqtrd 2times adantl 3eqtr4d zmulcl sylancr eqbrtrd ppncan 3anidm13
      1z mulid2 cc0 nnne0 ancoms df-ne sylib intnand gcdn0cl syl21anc syl111anc
      wne dvdsgcd mp2and cn0 mulgcd pythagtriplem3 2cn mulid1i syl6eq dvdsprime
      2nn0 orel1 sylc ) ADEZBDEZCDEZUAZAFUBGZBFUBGZHGZCFUBGZIZABJGKIZFALMZUCZNZ
      UAZCBOGZCBHGZJGZFIZUCUUGUUFKIZUDZUUHUUCUUGYTYMYRYSUUAUEUUCUUGNZFYNLMZYTUU
      JFYQYOOGZYNLUUJFUUEUUDPGZUULLUUJFUUELMZFUUMLMZUUCUUFUUELMZUUGUUNUUCUUFUUD
      LMZUUPUUCUUDSEZUUESEZUUQUUPNYMYRUURUUBYKYLUURYJYLCSEZBSEZUURYKCULZBULZCBU
      FZUGUHUIZUUCUUEDEZUUSUUCYLYKUVFYJYKYLYRUUBUJZYJYKYLYRUUBUKZCBUMZQUUEULRZU
      UDUUEUNQUOUUGUUPUUNUUFFUUELUPUQVBUUJUUSUURUUNUUOVCZUUJUUTUVAUUSUUJYLUUTYJ
      YKYLYRUUBUUGURZUVBRZUUJYKUVAYJYKYLYRUUBUUGUSZUVCRZCBUTQUUJUUTUVAUURUVMUVO
      UVDQFSEZUUSUURUVKVDFUUEUUDVAVEQVFUUJCTEZBTEZUULUUMIUUJYLUVQUVLCVNZRUUJYKU
      VRUVNBVNZRCBVGQVHUUJYPYOOGZUULYNUUJYPYQYOOYMYRUUBUUGVIVJUUJYNTEZYOTEZUWAY
      NIUUJYNDEZUWBUUJYJUWDYJYKYLYRUUBUUGVKAVLRYNVNRUUJYODEZUWCUUJYKUWEUVNBVLRY
      OVNRYNYOVMQVOVPUUJASEZUUKYTWEZUUCUWFUUGYMYRUWFUUBYJYKUWFYLAULUIUIVQFVREZU
      WFFDEUWGVSVTAFFWAWBRWCWDUUCUUFFLMZUUIUUCUUFFBPGZFCPGZJGZFLUUCUUFUWJLMZUUF
      UWKLMZUUFUWLLMZUUCUUFUUDUWJJGZUWJLUUCUUFUUDUUEKWFZUUDPGZHGZJGZUWPUUCUURUU
      SUUFUWTIZUVEUVJUWQSEUURUUSUXAKWGWHUWQUUDUUEWIVEQUUCUVQUVRUWTUWPIUUCYLUVQU
      VGUVSRZUUCYKUVRUVHUVTRZUVQUVRNZUWSUWJUUDJUXDUUEUUDOGZBBHGZUWSUWJUVQUVRUXE
      UXFICBBWJWKUXDUWSUUEUUDWFZHGZUXEUXDUWRUXGUUEHUXDUUDTEZUWRUXGICBWLZUUDWMRW
      NUXDUUETEUXIUXHUXEICBWOUXJUUEUUDWPQWQUVRUWJUXFIUVQBWRWSWTWNQWQUUCUWPUUDLM
      ZUWPUWJLMZUUCUURUWJSEZUXKUXLNUVEUUCUVPUVAUXMVDUUCYKUVAUVHUVCRZFBXAXBZUUDU
      WJUNQUOXCUUCUUFUUDUWKJGZUWKLUUCUUFUUDUUEKUUDPGZHGZJGZUXPUUCUURUUSUUFUXSIZ
      UVEUVJKSEUURUUSUXTXFKUUDUUEWIVEQUUCUXRUWKUUDJUUCUVQUVRUXRUWKIUXBUXCUXDUUE
      UUDHGZCCHGZUXRUWKUVQUVRUYAUYBICBCXDXEUXDUXQUUDUUEHUXDUXIUXQUUDIUXJUUDXGRW
      NUVQUWKUYBIUVRCWRVQWTQWNWQUUCUXPUUDLMZUXPUWKLMZUUCUURUWKSEZUYCUYDNUVEUUCU
      VPUUTUYEVDUUCYLUUTUVGUVBRZFCXAXBZUUDUWKUNQUOXCUUCUUFSEZUXMUYEUWMUWNNUWOVC
      UUCUUFDEZUYHUUCUURUUSUUDXHIZUUEXHIZNUCUYIUVEUVJUUCUYKUYJUUCUUEXHXQZUYKUCY
      MYRUYLUUBYKYLUYLYJYLYKUYLYLYKNUVFUYLUVIUUEXIRXJUHUIUUEXHXKXLXMUUDUUEXNXOZ
      UUFULRUXOUYGUUFUWJUWKXRXPXSUUCUWLFBCJGZPGZFUUCUVAUUTUWLUYOIZUXNUYFFXTEUVA
      UUTUYPYGFBCYAVEQUUCUYOFKPGFUUCUYNKFPABCYBWNFYCYDYEWQVPUUCUWHUYIUWIUUIWEVS
      UYMFUUFYFXBWCUUGUUHYHYI $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  $( Lemma for ~ pythagtrip .  Show that ` C - B ` is positive.  (Contributed
     by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem10 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) -> 0 < ( C - B ) ) $=
    ( cn wcel c2 cexp co clt wbr cc0 nnre 3ad2ant1 syl11anc resqcl syl 3ad2ant2
    cr wb adantr w3a caddc wceq wa cmin wne nnne0 sqgt0 ltaddpos2 mpbid breqtrd
    simpr cle cn0 nnnn0 nn0ge0 3ad2ant3 lt2sq syl22anc mpbird posdif ) ADEZBDEZ
    CDEZUAZAFGHZBFGHZUBHZCFGHZUCZUDZBCIJZKCBUEHIJZVKVLVGVIIJZVKVGVHVIIVEVGVHIJZ
    VJVEKVFIJZVOVEAREZAKUFZVPVBVCVQVDALMZVBVCVRVDAUGMAUHNVEVFREZVGREZVPVOSVEVQV
    TVSAOPVEBREZWAVCVBWBVDBLQZBOPVFVGUINUJTVEVJULUKVKWBKBUMJZCREZKCUMJZVLVNSVEW
    BVJWCTZVEWDVJVCVBWDVDVCBUNEWDBUOBUPPQTVEWEVJVDVBWEVCCLUQTZVEWFVJVDVBWFVCVDC
    UNEWFCUOCUPPUQTBCURUSUTVKWBWEVLVMSWGWHBCVANUJ $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C - B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem6 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C - B ) ) = ( ( C - B ) gcd A ) ) $=
    ( cn wcel w3a c2 cexp co wceq cgcd c1 cz nnz syl11anc 3ad2ant1 syl eqtrd cc
    cmin caddc cdivides wbr wn wa csqr cfv cn0 cc0 clt 3ad2ant3 3ad2ant2 zsubcl
    pythagtriplem10 3adant3 jca elnnz sylibr nnnn0 simp3 nnaddcl pythagtriplem4
    cmul simp2 3jca gcd12 zcn sqcl nncn pncan opreq1 eqtr3d subsq coprimeprodsq
    opreq1d mulcom sylc fveq2d cr cle gcdcl nn0re nn0ge0 sqrsq ) ADEZBDEZCDEZFZ
    AGHIZBGHIZUAIZCGHIZJZABKILJGAUBUCUDUEZFZCBTIZUFUGWPAKIZGHIZUFUGZWQWOWPWRUFW
    OWPUHEZCBUAIZMEZAUHEZFZWPXAKIZAKIZLJZUEWIWPXAVCIZJWPWRJWOXDXGWOWTXBXCWOWPDE
    ZWTWOWPMEZUIWPUJUCZUEXIWOXJXKWHWMXJWNWHCMEZBMEZXJWGWEXLWFCNUKWFWEXMWGBNULCB
    UMOZPZWHWMXKWNABCUNUOUPWPUQURWPUSQWHWMXBWNWHXADEZXBWHWGWFXPWEWFWGUTZWEWFWGV
    DZCBVAOZXANQPWHWMXCWNWEWFXCWGAUSPPVEWOXFLAKIZLWOXELAKABCVBVOWOAMEZXTLJWHWMY
    AWNWEWFYAWGANPZPZAVFQRUPWOWIWLWJTIZXHWOWKWJTIZWIYDWHWMYEWIJZWNWHWISEZWJSEZY
    FWHASEZYGWHYAYIYBAVGQAVHQWHBSEZYHWHWFYJXRBVIQZBVHQWIWJVJOPWMWHYEYDJWNWKWLWJ
    TVKULVLWHWMYDXHJWNWHYDXAWPVCIZXHWHCSEZYJYDYLJWHWGYMXQCVIQYKCBVMOWHXASEZWPSE
    ZYLXHJWHXPYNXSXAVIQWHXJYOXNWPVGQXAWPVPORPRWPXAAVNVQVRWOWQVSEZUIWQVTUCZWSWQJ
    WOWQUHEZYPWOXJYAYRXOYCWPAWAOZWQWBQWOYRYQYSWQWCQWQWDOR $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C + B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem7 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C + B ) ) = ( ( C + B ) gcd A ) ) $=
    ( cn wcel c2 cexp co wceq cgcd c1 cmin nnz syl syl11anc 3ad2ant1 eqtrd nncn
    cz cc w3a caddc cdivides wbr wn wa csqr cfv cmul simp3 simp2 zsubcl nnaddcl
    cn0 nnnn0 3jca pythagtriplem4 opreq1d gcd12 jca sqcl opreq1 3ad2ant2 eqtr3d
    pncan subsq zcn mulcom coprimeprodsq2 sylc fveq2d cr cc0 gcdcl nn0re nn0ge0
    cle sqrsq ) ADEZBDEZCDEZUAZAFGHZBFGHZUBHZCFGHZIZABJHKIFAUCUDUEUFZUAZCBUBHZU
    GUHWJAJHZFGHZUGUHZWKWIWJWLUGWICBLHZSEZWJUNEZAUNEZUAZWNWJJHZAJHZKIZUFWCWNWJU
    IHZIWJWLIWIWRXAWIWOWPWQWBWGWOWHWBCSEZBSEZWOWBWAXCVSVTWAUJZCMNWBVTXDVSVTWAUK
    ZBMNCBULOZPWBWGWPWHWBWJDEZWPWBWAVTXHXEXFCBUMOZWJUONPWBWGWQWHVSVTWQWAAUOPPUP
    WIWTKAJHZKWIWSKAJABCUQURWIASEZXJKIWBWGXKWHVSVTXKWAAMPPZAUSNQUTWIWCWFWDLHZXB
    WIWEWDLHZWCXMWBWGXNWCIZWHWBWCTEZWDTEZXOWBATEZXPVSVTXRWAARPAVANWBBTEZXQWBVTX
    SXFBRNZBVANWCWDVEOPWGWBXNXMIWHWEWFWDLVBVCVDWBWGXMXBIWHWBXMWJWNUIHZXBWBCTEZX
    SXMYAIWBWAYBXECRNXTCBVFOWBWJTEZWNTEZYAXBIWBXHYCXIWJRNWBWOYDXGWNVGNWJWNVHOQP
    QWNWJAVIVJVKWIWKVLEZVMWKVQUDZWMWKIWIWKUNEZYEWIWJSEZXKYGWBWGYHWHWBXHYHXIWJMN
    PXLWJAVNOZWKVONWIYGYFYIWKVPNWKVROQ $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)


  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C - B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem8 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C - B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr cmin cfv pythagtriplem6 zsubcl syl2anc 3adant1 wne nnne0 df-ne sylib
    csqr intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIM
    KGANUAOPZFCBUBIZULUCVCALIZDABCUDUTVAVDDEZVBUTVCTEZATEZVCQKZAQKZPOZVEURUSVFU
    QUSCTEBTEVFURCRBRCBUEUFUGUQURVGUSARSUQURVJUSUQVIVHUQAQUHVIOAUIAQUJUKUMSVCAU
    NUOSUP $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C + B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem9 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C + B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr csqr cfv pythagtriplem7 zaddcl syl2anc 3adant1 wne nnne0 df-ne sylib
    intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIMKGANU
    AOPZFZCBJIZUBUCVCALIZDABCUDVBVCTEZATEZVCQKZAQKZPOZVDDEUSUTVEVAUQURVEUPURCTE
    BTEVEUQCRBRCBUEUFUGSUSUTVFVAUPUQVFURARSSUSUTVIVAUPUQVIURUPVHVGUPAQUHVHOAUIA
    QUJUKULSSVCAUMUNUO $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)


  ${
    pythagtriplem11.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` M ` (which will eventually be
       closely related to the ` m ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem11 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> M e. NN ) $=
      ( cn wcel c2 co cdivides wbr wa cz cc0 clt nnz syl wb syl11anc 3ad2ant1
      w3a cexp caddc wceq cgcd c1 wn csqr cfv cmin cdiv pythagtriplem9 3ad2ant3
      simp3r 3ad2ant2 zaddcl dvdsgcdb mp3an1 simprd mtand pythagtriplem7 breq2d
      biimpar mtbird pythagtriplem8 zsubcl pythagtriplem6 opoe syl22anc nnaddcl
      2z wne 2ne0 divides2 mp3an12 mpbid cr nnre nngt0 addgt0 halfpos2 sylanbrc
      elnnz syl5eqel ) AFGZBFGZCFGZUAZAHUBIBHUBIUCICHUBIUDZABUEIUFUDZHAJKZUGZLZ
      UAZDCBUCIZUHUIZCBUJIZUHUIZUCIZHUKIZFEWNWTMGZNWTOKZWTFGWNHWSJKZXAWNWPMGZHW
      PJKZUGWRMGZHWRJKZUGXCWNWPFGZXDABCULZWPPQWNXEHWOAUEIZJKZWNXKWKWHWIWJWLUNZW
      NXKLHWOJKZWKWNXMWKLZXKWNWOMGZAMGZXNXKRZWHWIXOWMWHCMGZBMGZXOWGWEXRWFCPUMZW
      FWEXSWGBPUOZCBUPSTWHWIXPWMWEWFXPWGAPTTZHMGZXOXPXQVKHWOAUQURSVCUSUTWNWPXJH
      JABCVAVBVDWNWRFGZXFABCVEZWRPQWNXGHWQAUEIZJKZWNYGWKXLWNYGLHWQJKZWKWNYHWKLZ
      YGWNWQMGZXPYIYGRZWHWIYJWMWHXRXSYJXTYACBVFSTYBYCYJXPYKVKHWQAUQURSVCUSUTWNW
      RYFHJABCVGVBVDWPWRVHVIWNWSMGZXCXARZWNWSFGZYLWNXHYDYNXIYEWPWRVJSZWSPQYCHNV
      LYLYMVKVMHWSVNVOQVPWNNWSOKZXBWNWPVQGZWRVQGZNWPOKZNWROKZYPWNXHYQXIWPVRQWNY
      DYRYEWRVRQWNXHYSXIWPVSQWNYDYTYEWRVSQWPWRVTVIWNWSVQGZYPXBRWNYNUUAYOWSVRQWS
      WAQVPWTWCWBWD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` M ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem12 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( M ^ 2 ) = ( ( C + A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cdiv cmul syl syl11anc 3ad2ant1 cc0 2cn
      cc cn w3a cgcd c1 cdivides wn wa csqr cfv opreq1i axaddcl syl2anc 3adant1
      cmin nncn sqrcl subcl addcl wne 2ne0 mp3an23 sqvali opreq2i syl6eq binom2
      sqdiv cr cle nnre axaddrcl clt 3ad2ant3 3ad2ant2 nngt0 addgt0 syl22anc wi
      0re ltle mpan sylc sqsqr opreq1d resubcl pythagtriplem10 3adant3 opreq12d
      ppncan 2times eqtr4d opreq1 sqcl pncan subsq 3eqtr3rd fveq2d sqrmul nnnn0
      syl111anc nn0ge0 sqrsq 3eqtr3d opreq2d mulcl sylancr add32 mp3an1 3eqtr4d
      cn0 adddi 3eqtrd 3adant2 pm3.2i divdiv1 divcan3 3eqtr2d eqtrd syl5eq ) AU
      AFZBUAFZCUAFZUBZAGHIZBGHIZJIZCGHIZKZABUCIUDKGAUELUFUGZUBZDGHICBJIZUHUIZCB
      UNIZUHUIZJIZGMIZGHIZCAJIZGMIZDYOGHEUJYIYPYNGHIZGGNIZMIZYRYIYNTFZYPUUAKYBY
      GUUBYHYBYKTFZYMTFZUUBYBYJTFZUUCXTYAUUEXSYACTFZBTFZUUEXTCUOZBUOZCBUKULUMZY
      JUPOZYBYLTFZUUDXTYAUULXSYAUUFUUGUULXTUUHUUICBUQULUMZYLUPOZYKYMURPQUUBYPYS
      GGHIZMIZUUAUUBGTFZGRUSZYPUUPKSUTYNGVFVAUUOYTYSMGSVBVCVDOYIUUAGYQNIZYTMIZU
      USGMIZGMIZYRYIYSUUSYTMYIYSYKGHIZGYKYMNIZNIZJIZYMGHIZJIZYJUVEJIZYLJIZUUSYI
      UUCUUDYSUVHKYBYGUUCYHUUKQYBYGUUDYHUUNQYKYMVEPYIUVFUVIUVGYLJYIUVCYJUVEJYIY
      JVGFZRYJVHLZUVCYJKYBYGUVKYHXTYAUVKXSYACVGFZBVGFZUVKXTCVIZBVIZCBVJULUMQZYI
      UVKRYJVKLZUVLUVQYBYGUVRYHYBUVMUVNRCVKLZRBVKLZUVRYAXSUVMXTUVOVLXTXSUVNYAUV
      PVMYAXSUVSXTCVNVLXTXSUVTYABVNVMCBVOVPQRVGFZUVKUVRUVLVQVRRYJVSVTWAZYJWBPWC
      YIYLVGFZRYLVHLZUVGYLKYBYGUWCYHXTYAUWCXSYAUVMUVNUWCXTUVOUVPCBWDULUMQZYIUWC
      RYLVKLZUWDUWEYBYGUWFYHABCWEWFUWAUWCUWFUWDVQVRRYLVSVTWAZYLWBPWGYIYJYLJIZUV
      EJIZGCNIZGANIZJIZUVJUUSYIUWHUWJUVEUWKJYIUWHCCJIZUWJYIUUFUUGUUFUWHUWMKYBYG
      UUFYHYAXSUUFXTUUHVLQZYBYGUUGYHXTXSUUGYAUUIVMQZUWNCBCWHWSYIUUFUWJUWMKUWNCW
      IOWJYIUVDAGNYIYJYLNIZUHUIZYCUHUIZUVDAYIUWPYCUHYIYEYDUNIZYFYDUNIZYCUWPYGYB
      UWSUWTKYHYEYFYDUNWKVMYIYCTFZYDTFZUWSYCKYIATFZUXAYBYGUXCYHXSXTUXCYAAUOZQQZ
      AWLOYIUUGUXBUWOBWLOYCYDWMPYIUUFUUGUWTUWPKUWNUWOCBWNPWOWPYIUVKUVLUWCUWDUWQ
      UVDKUVQUWBUWEUWGYJYLWQVPYIAVGFZRAVHLZUWRAKYBYGUXFYHXSXTUXFYAAVIQQYBYGUXGY
      HXSXTUXGYAXSAXIFUXGAWRAWTOQQAXAPXBXCWGYIUUEUVETFZUULUVJUWIKYBYGUUEYHUUJQY
      BYGUXHYHYBUUQUVDTFZUXHSYBUUCUUDUXIUUKUUNYKYMXDPGUVDXDXEQYBYGUULYHUUMQYJUV
      EYLXFWSYIUUFUXCUUSUWLKZUWNUXEUUQUUFUXCUXJSGCAXJXGPXHXKWCYIUUSTFZUVBUUTKZY
      IUUQYQTFZUXKSYBYGUXMYHXSYAUXMXTYAUUFUXCUXMXSUUHUXDCAURULXLQZGYQXDXEUXKUUQ
      UURUGZUXOUXLUUQUURSUTXMZUXPUUSGGXNVAOYIUVAYQGMYIUXMUVAYQKZUXNUXMUUQUURUXQ
      SUTYQGXOVAOWCXPXQXR $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    pythagtriplem13.1 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` N ` (which will eventually be
       closely related to the ` n ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem13 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> N e. NN ) $=
      ( cn wcel c2 co cdivides wbr cz cc0 clt nnz syl wb syl11anc 3ad2ant1 cr
      w3a cexp caddc wceq cgcd c1 wn wa csqr cfv cmin cdiv pythagtriplem9 simp3
      simp3r simp2 nnaddcl dvdsgcdb mp3an1 biimpar simprd pythagtriplem7 breq2d
      2z mtbird pythagtriplem8 3ad2ant3 3ad2ant2 zsubcl pythagtriplem6 syl22anc
      mtand omoe zre simp13 nnre nnrp ltsubrp nngt0 simp12 ltaddpos mpbid lttrd
      crp cle pythagtriplem10 3adant3 0re ltle mpan sylc addgt0 sqrlt nnsub wne
      wi 2ne0 divides2 mp3an12 halfpos2 elnnz sylanbrc syl5eqel ) AFGZBFGZCFGZU
      AZAHUBIBHUBIUCICHUBIUDZABUEIUFUDZHAJKZUGZUHZUAZDCBUCIZUIUJZCBUKIZUIUJZUKI
      ZHULIZFEXMXSLGZMXSNKZXSFGXMHXRJKZXTXMXOLGZHXOJKZUGXQLGZHXQJKZUGYBXMXOFGZY
      CABCUMZXOOPXMYDHXNAUEIZJKZXMYJXJXGXHXIXKUOZXMYJUHHXNJKZXJXMYLXJUHZYJXMXNL
      GZALGZYMYJQZXGXHYNXLXGXNFGZYNXGXFXEYQXDXEXFUNXDXEXFUPCBUQRZXNOPSXGXHYOXLX
      DXEYOXFAOSSZHLGZYNYOYPVDHXNAURUSRUTVAVLXMXOYIHJABCVBVCVEXMXQFGZYEABCVFZXQ
      OPXMYFHXPAUEIZJKZXMUUDXJYKXMUUDUHHXPJKZXJXMUUEXJUHZUUDXMXPLGZYOUUFUUDQZXG
      XHUUGXLXGCLGZBLGZUUGXFXDUUIXECOVGXEXDUUJXFBOVHCBVIRZSYSYTUUGYOUUHVDHXPAUR
      USRUTVAVLXMXQUUCHJABCVJVCVEXOXQVMVKXMXRLGZYBXTQZXMXRFGZUULXMXQXONKZUUNXMX
      PXNNKZUUOXMXPCXNXGXHXPTGZXLXGUUGUUQUUKXPVNPSZXMXFCTGZXDXEXFXHXLVOCVPPZXGX
      HXNTGZXLXGYQUVAYRXNVPPSZXMUUSBWDGZXPCNKUUTXGXHUVCXLXEXDUVCXFBVQVHSCBVRRXM
      MBNKZCXNNKZXGXHUVDXLXEXDUVDXFBVSVHSZXMBTGZUUSUVDUVEQXMXEUVGXDXEXFXHXLVTBV
      PPZUUTBCWARWBWCXMUUQMXPWEKZUVAMXNWEKZUUPUUOQUURXMUUQMXPNKZUVIUURXGXHUVKXL
      ABCWFWGMTGZUUQUVKUVIWPWHMXPWIWJWKUVBXMUVAMXNNKZUVJUVBXMUUSUVGMCNKZUVDUVMU
      UTUVHXGXHUVNXLXFXDUVNXECVSVGSUVFCBWLVKUVLUVAUVMUVJWPWHMXNWIWJWKXPXNWMVKWB
      XMUUAYGUUOUUNQUUBYHXQXOWNRWBZXROPYTHMWOUULUUMVDWQHXRWRWSPWBXMMXRNKZYAXMUU
      NUVPUVOXRVSPXMXRTGZUVPYAQXMUUNUVQUVOXRVPPXRWTPWBXSXAXBXC $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` N ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem14 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( N ^ 2 ) = ( ( C - A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cmin cdiv syl syl11anc 3ad2ant1 cc0 cmul
      cc cn w3a cgcd c1 cdivides wn csqr opreq1i nncn addcl syl2anc sqrcl subcl
      cfv 3adant1 wne 2cn 2ne0 sqdiv mp3an23 sqvali opreq2i sqcl pm3.2i divdiv1
      wa simp12 simp13 binom2sub cr cle axaddrcl clt adantl adantr nngt0 addgt0
      nnre syl22anc wi 0re ltle mpan sylc sqsqr opreq1d resubcl pythagtriplem10
      3adant3 opreq12d simp11 subdi mp3an1 ppncan 3anidm13 2times eqtr4d opreq1
      3ad2ant2 pncan subsq fveq2d sqrmul cn0 nnnn0 nn0ge0 sqrsq 3eqtr3d opreq2d
      3eqtr3rd recnd mulcl sylancr addsub 3eqtr2rd 3eqtrd 3adant2 divcan3 eqtrd
      syl111anc eqtr3d syl5eq ) AUAFZBUAFZCUAFZUBZAGHIZBGHIZJIZCGHIZKZABUCIUDKG
      AUELUFVFZUBZDGHICBJIZUGUNZCBMIZUGUNZMIZGNIZGHIZCAMIZGNIZDYSGHEUHYMYTYRGHI
      ZGGHIZNIZUUBYMYRTFZYTUUEKZYFYKUUFYLYDYEUUFYCYDYEVFZYOTFZYQTFZUUFUUHYNTFZU
      UIYECTFZBTFZUUKYDCUIZBUIZCBUJUKYNULOZUUHYPTFZUUJYEUULUUMUUQYDUUNUUOCBUMUK
      YPULOZYOYQUMPUOQZUUFGTFZGRUPZUUGUQURYRGUSUTOYMUUEUUCGGSIZNIZUUBUUDUVBUUCN
      GUQVAVBYMUUCGNIZGNIZUVCUUBYMUUCTFZUVEUVCKZYMUUFUVFUUSYRVCOUVFUUTUVAVFZUVH
      UVGUUTUVAUQURVDZUVIUUCGGVEUTOYMUVDUUAGNYMUVDGUUASIZGNIZUUAYMUUCUVJGNYMUUC
      YOGHIZGYOYQSIZSIZMIZYQGHIZJIZYNUVNMIZYPJIZUVJYMUUIUUJUUCUVQKYMYDYEUUIYCYD
      YEYKYLVGZYCYDYEYKYLVHZUUPPYMYDYEUUJUVTUWAUURPYOYQVIPYMUVOUVRUVPYPJYMUVLYN
      UVNMYMYNVJFZRYNVKLZUVLYNKYFYKUWBYLYDYEUWBYCYECVJFZBVJFZUWBYDCVRZBVRZCBVLU
      KZUOQZYFYKUWCYLYDYEUWCYCUUHUWBRYNVMLZUWCUWHUUHUWDUWERCVMLZRBVMLZUWJYEUWDY
      DUWFVNYDUWEYEUWGVOYEUWKYDCVPVNYDUWLYEBVPVOCBVQVSRVJFZUWBUWJUWCVTWARYNWBWC
      WDUOQZYNWEPWFYMYPVJFZRYPVKLZUVPYPKYFYKUWOYLYDYEUWOYCYEUWDUWEUWOYDUWFUWGCB
      WGUKUOQZYMUWORYPVMLZUWPUWQYFYKUWRYLABCWHWIUWMUWOUWRUWPVTWARYPWBWCWDZYPWEP
      WJYMUVJGCSIZGASIZMIZYNYPJIZUVNMIZUVSYMUULATFZUVJUXBKZYMYEUULUWAUUNOZYMYCU
      XEYCYDYEYKYLWKAUIZOUUTUULUXEUXFUQGCAWLWMPYMUXCUWTUVNUXAMYFYKUXCUWTKZYLYDY
      EUXIYCYEUULUUMUXIYDUUNUUOUULUUMVFUXCCCJIZUWTUULUUMUXCUXJKCBCWNWOUULUWTUXJ
      KUUMCWPVOWQUKUOQYMUVMAGSYMYNYPSIZUGUNZYGUGUNZUVMAYMUXKYGUGYMYIYHMIZYJYHMI
      ZYGUXKYKYFUXNUXOKYLYIYJYHMWRWSYFYKUXNYGKZYLYFYGTFZYHTFZUXPYCYDUXQYEYCUXEU
      XQUXHAVCOQYDYCUXRYEYDUUMUXRUUOBVCOWSYGYHWTPQYMUULUUMUXOUXKKUXGYMYDUUMUVTU
      UOOCBXAPXJXBYMUWBUWCUWOUWPUXLUVMKUWIUWNUWQUWSYNYPXCVSYMAVJFZRAVKLZUXMAKYF
      YKUXSYLYCYDUXSYEAVRQQYFYKUXTYLYCYDUXTYEYCAXDFUXTAXEAXFOQQAXGPXHXIWJYMUUKU
      UQUVNTFZUXDUVSKYMYNUWIXKYMYPUWQXKYFYKUYAYLYFUUTUVMTFZUYAUQYFUUIUUJUYBYDYE
      UUIYCUUPUOYDYEUUJYCUURUOYOYQXLPGUVMXLXMQYNYPUVNXNXTXOXPWFYMUUATFZUVKUUAKZ
      YFYKUYCYLYCYEUYCYDYEUULUXEUYCYCUUNUXHCAUMUKXQQUYCUUTUVAUYDUQURUUAGXRUTOXS
      WFYAYBXSYB $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    pythagtriplem15.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    pythagtriplem15.2 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` A ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem15 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> A = ( ( M ^ 2 ) - ( N ^ 2 ) ) ) $=
      ( cn wcel c2 cexp co caddc wceq cmin cdiv cc syl11anc syl 3ad2ant1 w3a c1
      cgcd cdivides wbr wn pythagtriplem12 pythagtriplem14 opreq12d simp3 simp1
      wa nnaddcl nncn nnz 3ad2ant3 zsubcl zcn cc0 wne 2cn 2ne0 pm3.2i divsubdir
      mp3an3 cmul pnncan syl111anc 2times eqtr4d opreq1d divcan3 mp3an23 eqtr3d
      cz eqtrd eqtr2d ) AHIZBHIZCHIZUAZAJKLBJKLMLCJKLNZABUCLUBNJAUDUEUFULZUAZDJ
      KLZEJKLZOLCAMLZJPLZCAOLZJPLZOLZAWDWEWHWFWJOABCDFUGABCEGUHUIWDWGWIOLZJPLZW
      KAWDWGQIZWIQIZWMWKNZWAWBWNWCWAWGHIZWNWAVTVRWQVRVSVTUJVRVSVTUKCAUMRWGUNSTW
      AWBWOWCWAWIVOIZWOWACVOIZAVOIZWRVTVRWSVSCUOUPVRVSWTVTAUOTCAUQRWIURSTWNWOJQ
      IZJUSUTZULWPXAXBVAVBVCWGWIJVDVERWDWMJAVFLZJPLZAWDWLXCJPWDWLAAMLZXCWDCQIZA
      QIZXGWLXENWAWBXFWCVTVRXFVSCUNUPTWAWBXGWCVRVSXGVTAUNTTZXHCAAVGVHWDXGXCXENX
      HAVISVJVKWDXGXDANZXHXGXAXBXIVAVBAJVLVMSVPVNVQ $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` B ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem16 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> B = ( 2 x. ( M x. N ) ) ) $=
      ( wcel c2 co wceq wbr cmul cdiv cc syl syl11anc 3adant1 3ad2ant1 cc0 cexp
      cn w3a caddc cgcd c1 cdivides wn wa csqr cmin opreq12i nncn addcl syl2anc
      cfv sqrcl subcl wne 2cn pm3.2i divmuldiv mpanr12 mulcl divdiv1 mp3an23 cr
      2ne0 cle nnre axaddrcl clt adantl adantr addgt0 syl22anc wi 0re ltle mpan
      nngt0 sylc resubcl pythagtriplem10 3adant3 opreq12d opreq1d simp12 simp13
      sqsqr subsq pnncan 3anidm23 2times eqtr4d divcan3 3eqtr3d 3eqtr2d opreq2d
      eqtrd syl5eq divcan2 3ad2ant2 eqtr2d ) AUBHZBUBHZCUBHZUCZAIUAJBIUAJUDJCIU
      AJKZABUEJUFKIAUGLUHUIZUCZIDEMJZMJIBINJZMJZBXKXLXMIMXKXLCBUDJZUJUPZCBUKJZU
      JUPZUDJZINJZXPXRUKJZINJZMJZXMDXTEYBMFGULXKYCXSYAMJZIIMJNJZYDINJZINJZXMXKX
      SOHZYAOHZYCYEKZXHXIYHXJXFXGYHXEXFXGUIZXPOHZXROHZYHYKXOOHZYLXGCOHZBOHZYNXF
      CUMZBUMZCBUNUOXOUQPZYKXQOHZYMXGYOYPYTXFYQYRCBURUOXQUQPZXPXRUNQZRSXHXIYIXJ
      XFXGYIXEYKYLYMYIYSUUAXPXRURQZRSYHYIUIIOHZITUSZUIZUUFYJUUDUUEUTVHVAZUUGXSY
      AIIVBVCQXKYDOHZYGYEKZXHXIUUHXJXFXGUUHXEYKYHYIUUHUUBUUCXSYAVDQRSUUHUUFUUFU
      UIUUGUUGYDIIVEVFPXKYFBINXKXPIUAJZXRIUAJZUKJZINJXOXQUKJZINJZYFBXKUULUUMINX
      KUUJXOUUKXQUKXKXOVGHZTXOVILZUUJXOKXHXIUUOXJXFXGUUOXEXGCVGHZBVGHZUUOXFCVJZ
      BVJZCBVKUOZRSXHXIUUPXJXFXGUUPXEYKUUOTXOVLLZUUPUVAYKUUQUURTCVLLZTBVLLZUVBX
      GUUQXFUUSVMXFUURXGUUTVNXGUVCXFCWAVMXFUVDXGBWAVNCBVOVPTVGHZUUOUVBUUPVQVRTX
      OVSVTWBRSXOWJQXKXQVGHZTXQVILZUUKXQKXHXIUVFXJXFXGUVFXEXGUUQUURUVFXFUUSUUTC
      BWCUORSZXKUVFTXQVLLZUVGUVHXHXIUVIXJABCWDWEUVEUVFUVIUVGVQVRTXQVSVTWBXQWJQW
      FWGXKUULYDINXKYLYMUULYDKXKXFXGYLXEXFXGXIXJWHZXEXFXGXIXJWIZYSQXKXFXGYMUVJU
      VKUUAQXPXRWKQWGXKUUNIBMJZINJZBXKUUMUVLINXHXIUUMUVLKZXJXFXGUVNXEXGYOYPUVNX
      FYQYRYOYPUIUUMBBUDJZUVLYOYPUUMUVOKCBBWLWMYPUVLUVOKYOBWNVMWOUORSWGXKYPUVMB
      KZXKXFYPUVJYRPYPUUDUUEUVPUTVHBIWPVFPWTWQWGWRXAWSXHXIXNBKZXJXFXEUVQXGXFYPU
      VQYRYPUUDUUEUVQUTVHBIXBVFPXCSXD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` C ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem17 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> C = ( ( M ^ 2 ) + ( N ^ 2 ) ) ) $=
      ( cn wcel w3a c2 cexp co caddc wceq wa cdiv cc 3ad2ant1 syl11anc cdivides
      cgcd c1 cmin pythagtriplem12 pythagtriplem14 opreq12d nncn 3ad2ant3 addcl
      wbr wn cc0 wne 2cn 2ne0 pm3.2i divdir mp3an3 cmul ppncan syl111anc 2times
      subcl syl eqtr4d opreq1d divcan3 mp3an23 eqtrd eqtr3d eqtr2d ) AHIZBHIZCH
      IZJZAKLMBKLMNMCKLMOZABUBMUCOKAUAUKULPZJZDKLMZEKLMZNMCANMZKQMZCAUDMZKQMZNM
      ZCVSVTWCWAWENABCDFUEABCEGUFUGVSWBWDNMZKQMZWFCVSWBRIZWDRIZWHWFOZVSCRIZARIZ
      WIVPVQWLVRVOVMWLVNCUHUISZVPVQWMVRVMVNWMVOAUHSSZCAUJTVSWLWMWJWNWOCAVDTWIWJ
      KRIZKUMUNZPWKWPWQUOUPUQWBWDKURUSTVSWHKCUTMZKQMZCVSWGWRKQVSWGCCNMZWRVSWLWM
      WLWGWTOWNWOWNCACVAVBVSWLWRWTOWNCVCVEVFVGVSWLWSCOZWNWLWPWQXAUOUPCKVHVIVEVJ
      VKVL $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    $d A m n $.  $d B m n $.  $d C m n $.
    $( Lemma for ~ pythagtrip .  Wrap the previous ` M ` and ` N ` up in
       quanitifers.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem18 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       E. n e. NN E. m e. NN
       ( A = ( ( m ^ 2 ) - ( n ^ 2 ) ) /\
         B = ( 2 x. ( m x. n ) ) /\
         C = ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) $=
      ( cn wcel w3a c2 cexp caddc wceq csqr cfv cmin cmul opreq1 opreq2d eqeq2d
      co cgcd c1 cdivides wbr wn cdiv wrex eqid pythagtriplem13 pythagtriplem11
      pythagtriplem15 pythagtriplem16 pythagtriplem17 opreq2 3anbi123d rcla42ev
      wa cv opreq1d syl113anc ) AFGBFGCFGHAIJTBIJTKTCIJTLABUATUBLIAUCUDUEUQHCBK
      TMNZCBOTMNZOTIUFTZFGVAVBKTIUFTZFGAVDIJTZVCIJTZOTZLZBIVDVCPTZPTZLZCVEVFKTZ
      LZADURZIJTZEURZIJTZOTZLZBIVNVPPTZPTZLZCVOVQKTZLZHZDFUGEFUGABCVCVCUHZUIABC
      VDVDUHZUJABCVDVCWGWFUKABCVDVCWGWFULABCVDVCWGWFUMWEVHVKVMHAVOVFOTZLZBIVNVC
      PTZPTZLZCVOVFKTZLZHEDVCVDFFVPVCLZVSWIWBWLWDWNWOVRWHAWOVQVFVOOVPVCIJQZRSWO
      WAWKBWOVTWJIPVPVCVNPUNRSWOWCWMCWOVQVFVOKWPRSUOVNVDLZWIVHWLVKWNVMWQWHVGAWQ
      VOVEVFOVNVDIJQZUSSWQWKVJBWQWJVIIPVNVDVCPQRSWQWMVLCWQVOVEVFKWRUSSUOUPUT $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A m n k $.  $d B m n k $.  $d C m n k $.
    $( Lemma for ~ pythagtrip .  Introduce ` k ` and remove the relative
       primality requirement.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem19 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       -. 2 || ( A / ( A gcd B ) ) ) ->
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) $=
      ( cn wcel c2 cexp co wceq wbr cmul wrex cz cc0 3ad2ant1 syl syl111anc w3a
      caddc cgcd cdiv cdivides wn cv cmin wa anim12i nnne0 df-ne sylib intnanrd
      nnz wne adantr gcdn0cl syl11anc 3adant3 c1 gcddvds syl2an simpld divides2
      clt wb mpbid cr nnre nngt0 divgt0 syl22anc elnnz sylanbrc simprd 3ad2ant2
      dvdssq anbi12d wi nnsqcl dvds2add simpr breqtrd 3ad2ant3 mpbird opreq1 cc
      nncn sqdiv opreq12d divdir syl112anc eqtr4d 3eqtr4d gcddiv syl31anc divid
      mpd eqtr3d pythagtriplem18 syl312anc divcan2 eqcomd 3jca opreq2 3anbi123d
      simp3 eqeq2d syl5ibcom reximdv 2rexbidv rcla4ev rexcom rexbii bitri ) AGH
      ZBGHZCGHZUAZAIJKZBIJKZUBKZCIJKZLZIAABUCKZUDKZUEMUFZUAZADUGZEUGZIJKZFUGZIJ
      KZUHKZNKZLZBYJIYKYMNKNKZNKZLZCYJYLYNUBKZNKZLZUAZEGOZFGOZDGOZUUDDGOEGOZFGO
      ZYIYFGHZAYFYONKZLZBYFYRNKZLZCYFUUANKZLZUAZEGOZFGOZUUGXTYEUUJYHXQXRUUJXSXQ
      XRUIAPHZBPHZUIAQLZBQLZUIUFZUUJXQUUTXRUVAAUOZBUOZUJXQUVDXRXQUVBUVCXQAQUPUV
      BUFAUKAQULUMUNUQABURUSUTZRYIYGYOLZBYFUDKZYRLZCYFUDKZUUALZUAZEGOZFGOZUUSYI
      YGGHZUVIGHZUVKGHZYGIJKZUVIIJKZUBKZUVKIJKZLYGUVIUCKZVALZYHUVOXTYEUVPYHXTYG
      PHZQYGVFMZUVPXTYFAUEMZUWEXTUWGYFBUEMZXQXRUWGUWHUIZXSXQUUTUVAUWIXRUVEUVFAB
      VBVCUTZVDXTYFPHZYFQUPZUUTUWGUWEVGXTUUJUWKUVGYFUOSZXTUUJUWLUVGYFUKSZXQXRUU
      TXSUVERZYFAVETVHXTAVIHZQAVFMZYFVIHZQYFVFMZUWFXQXRUWPXSAVJRXQXRUWQXSAVKRXT
      UUJUWRUVGYFVJSZXTUUJUWSUVGYFVKSZAYFVLVMYGVNVORXTYEUVQYHXTUVIPHZQUVIVFMZUV
      QXTUWHUXBXTUWGUWHUWJVPXTUWKUWLUVAUWHUXBVGUWMUWNXRXQUVAXSUVFVQZYFBVETVHXTB
      VIHZQBVFMZUWRUWSUXCXRXQUXEXSBVJVQXRXQUXFXSBVKVQUWTUXABYFVLVMUVIVNVORXTYEU
      VRYHXTYEUIZUVKPHZQUVKVFMZUVRUXGYFCUEMZUXHUXGUXJYFIJKZYDUEMZUXGUXKYCYDUEXT
      UXKYCUEMZYEXTUXKYAUEMZUXKYBUEMZUIZUXMXTUWIUXPUWJXTUWGUXNUWHUXOXTUWKUUTUWG
      UXNVGUWMUWOYFAVRUSXTUWKUVAUWHUXOVGUWMUXDYFBVRUSVSVHXTUXKPHZYAPHZYBPHZUXPU
      XMVTXTUXKGHZUXQXTUUJUXTUVGYFWASZUXKUOSXTYAGHZUXRXQXRUYBXSAWARZYAUOSXTYBGH
      ZUXSXRXQUYDXSBWAVQZYBUOSUXKYAYBWBTWSUQXTYEWCWDXTUXJUXLVGZYEXTUWKCPHZUYFUW
      MXSXQUYGXRCUOWEZYFCVRUSUQWFXTUXJUXHVGZYEXTUWKUWLUYGUYIUWMUWNUYHYFCVETUQVH
      XTUXIYEXTCVIHZQCVFMZUWRUWSUXIXSXQUYJXRCVJWEXSXQUYKXRCVKWEUWTUXACYFVLVMUQU
      VKVNVOUTYIYCUXKUDKZYDUXKUDKZUWAUWBYEXTUYLUYMLYHYCYDUXKUDWGVQYIUWAYAUXKUDK
      ZYBUXKUDKZUBKZUYLXTYEUWAUYPLYHXTUVSUYNUVTUYOUBXTAWHHZYFWHHZUWLUVSUYNLXQXR
      UYQXSAWIRZXTUUJUYRUVGYFWISZUWNAYFWJTXTBWHHZUYRUWLUVTUYOLXRXQVUAXSBWIVQZUY
      TUWNBYFWJTWKRXTYEUYLUYPLZYHXTYAWHHZYBWHHZUXKWHHZUXKQUPZVUCXTUYBVUDUYCYAWI
      SXTUYDVUEUYEYBWISXTUXTVUFUYAUXKWISXTUXTVUGUYAUXKUKSYAYBUXKWLWMRWNXTYEUWBU
      YMLZYHXTCWHHZUYRUWLVUHXSXQVUIXRCWIWEZUYTUWNCYFWJTRWOXTYEUWDYHXTYFYFUDKZUW
      CVAXTUUTUVAUUJUWIVUKUWCLUWOUXDUVGUWJABYFWPWQXTUYRUWLVUKVALUYTUWNYFWRUSWTR
      XTYEYHXHYGUVIUVKEFXAXBYIUVNUURFGYIUVMUUQEGYIAYFYGNKZLZBYFUVINKZLZCYFUVKNK
      ZLZUAZUVMUUQXTYEVURYHXTVUMVUOVUQXTVULAXTUYQUYRUWLVULALUYSUYTUWNAYFXCTXDXT
      VUNBXTVUAUYRUWLVUNBLVUBUYTUWNBYFXCTXDXTVUPCXTVUIUYRUWLVUPCLVUJUYTUWNCYFXC
      TXDXERUVMVUMUULVUOUUNVUQUUPUVHUVJVUMUULVGUVLUVHVULUUKAYGYOYFNXFXIRUVJUVHV
      UOUUNVGUVLUVJVUNUUMBUVIYRYFNXFXIVQUVLUVHVUQUUPVGUVJUVLVUPUUOCUVKUUAYFNXFX
      IWEXGXJXKXKWSUUFUUSDYFGYJYFLZUUDUUQFEGGVUSYQUULYTUUNUUCUUPVUSYPUUKAYJYFYO
      NWGXIVUSYSUUMBYJYFYRNWGXIVUSUUBUUOCYJYFUUANWGXIXGXLXMUSUUGUUEDGOZFGOUUIUU
      EDFGGXNVUTUUHFGUUDDEGGXNXOXPUM $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A k m n $.  $d B k m n $.  $d C k m n $.
    $( Parameterize the Pythagorean triples.  If ` A ` , ` B ` , and ` C ` are
       naturals, then they obey the Pythagorean triple formula iff they are
       parameterized by three naturals.  This proof follows the Isabelle proof
       at ~ http://afp.sourceforge.net/entries/Fermat3_4.shtml .  (Contributed
       by Scott Fenton, 19-Apr-2014.) $)
    pythagtrip $p |- ( ( A e. NN /\ B e. NN /\ C e. NN ) ->
    ( ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) <->
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) ) $=
      ( cn wcel w3a c2 cexp co wceq cmul wa wrex wo cdiv cdivides 3adant3 caddc
      cpr cv cmin cgcd wn divgcdodd adantr pythagtriplem19 3expia simp12 simp11
      wbr simp13 cc nnsqcl nncn 3ad2ant1 3ad2ant2 addcom syl11anc eqeq1d biimpa
      syl cz nnz gcdcom opreq2d breq2d notbid biimp3a syl311anc orim12d mpd cvv
      wb preq12bg mpanr12 anbi1d rexbidv 2rexbidv df-3an orbi12i 3ancoma orbi2i
      oprex andir 3bitr2i rexbii 2rexbii r19.43 3bitri syl6bb ex pythagtriplem2
      bitri mpbird wi impbid ) AGHZBGHZCGHZIZAJKLZBJKLZUALZCJKLZMZABUBDUCZEUCZJ
      KLZFUCZJKLZUDLZNLZXIJXJXLNLNLZNLZUBMZCXIXKXMUALNLMZOZDGPZEGPFGPZXCXHYBXCX
      HOZYBAXOMZBXQMZXSIZDGPZEGPZFGPZBXOMZAXQMZXSIZDGPZEGPZFGPZQZYCJAABUELZRLSU
      MUFZJBYQRLZSUMZUFZQZYPXCUUBXHWTXAUUBXBABUGTUHYCYRYIUUAYOXCXHYRYIABCDEFUIU
      JXCXHUUAYOXCXHUUAIXAWTXBXEXDUALZXGMZJBBAUELZRLZSUMZUFZYOWTXAXBXHUUAUKWTXA
      XBXHUUAULWTXAXBXHUUAUNXCXHUUDUUAXCXHUUDXCXFUUCXGXCXDUOHZXEUOHZXFUUCMWTXAU
      UIXBWTXDGHUUIAUPXDUQVDURXAWTUUJXBXAXEGHUUJBUPXEUQVDUSXDXEUTVAVBVCTXCXHUUA
      UUHYCYTUUGYCYSUUFJSYCYQUUEBRYCAVEHZBVEHZYQUUEMXCUUKXHWTXAUUKXBAVFURUHXCUU
      LXHXAWTUULXBBVFUSUHABVGVAVHVIVJVKBACDEFUIVLUJVMVNXCYBYPVPZXHWTXAUUMXBWTXA
      OZYBYDYEOZYKYJOZQZXSOZDGPZEGPFGPZYPUUNYAUUSFEGGUUNXTUURDGUUNXRUUQXSUUNXOV
      OHXQVOHXRUUQVPXIXNNWFXIXPNWFABXOXQGGVOVOVQVRVSVTWAUUTYFYLQZDGPZEGPFGPYGYM
      QZEGPZFGPZYPUUSUVBFEGGUURUVADGUURUUOXSOZUUPXSOZQYFYKYJXSIZQUVAUUOUUPXSWGY
      FUVFUVHUVGYDYEXSWBYKYJXSWBWCUVHYLYFYKYJXSWDWEWHWIWJUVBUVCFEGGYFYLDGWKWJUV
      EYHYNQZFGPYPUVDUVIFGYGYMEGWKWIYHYNFGWKWPWLWMTUHWQWNWTXAYBXHWRXBABCDEFWOTW
      S $.
      $( [19-Apr-2014] $) $( [19-Apr-2014] $)
  $}

