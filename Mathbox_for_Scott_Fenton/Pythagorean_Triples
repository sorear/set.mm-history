$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Pythagorean Triples
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A n m k $.  $d B n m k $.  $d C n m k $.
    $( Lemma for ~ pythagtrip .  Prove a weaker version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem1 $p |- (
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) $=
      ( c2 cexp co cmin cmul wceq caddc cn wcel cc sqcl syl 2cn c4 cv w3a wa wi
      wrex nncn adantl axmulcl syl2anr mulcl sylancr subcl syl2anc adantr add32
      syl3anc subadd23 sqmul sq2 a1i opreq12d eqtrd opreq1d 4re recni subaddrii
      ancoms 2p2e4 opreq1i subdir mp3an12 syl5reqr opreq2d binom2 3eqtr4d simp3
      binom2sub 3ad2ant2 3ad2ant1 3ad2ant3 adddi eqtr4d addcl syl3an opreqan12d
      3adant3 opreq1 eqeq12d syl5ibrcom 3expa rexlimdva rexlimivv ) ADUAZEUAZGH
      IZFUAZGHIZJIZKIZLZBWMGWNWPKIZKIZKIZLZCWMWOWQMIZKIZLZUBZDNUEAGHIZBGHIZMIZC
      GHIZLZFENNWPNOZWNNOZUCXHXMDNXNXOWMNOZXHXMUDXNXOXPUBXMXHWSGHIZXCGHIZMIZXFG
      HIZLZXNWPPOZXOWNPOZXPWMPOZYAWPUFWNUFWMUFYBYCYDUBZWMGHIZWRGHIZXBGHIZMIZKIZ
      YFXEGHIZKIZXSXTYEYIYKYFKYBYCYIYKLYDYBYCUCZWOGHIZGWOWQKIZKIZJIZWQGHIZMIZYH
      MIZYNYPMIZYRMIZYIYKYMYTYQYHMIZYRMIZUUBYMYQPOZYRPOZYHPOZYTUUDLYMYNPOZYPPOZ
      UUEYMWOPOZUUHYCUUJYBWNQZUGWOQRZYMGPOZYOPOZUUISYCUUJWQPOZUUNYBUUKWPQZWOWQU
      HUIZGYOUJUKZYNYPULUMYMUUOUUFYBUUOYCUUPUNWQQRYMXBPOZUUGYMUUMXAPOZUUSSYCYBU
      UTWNWPUHVGZGXAUJZUKXBQZRZYQYRYHUOUPYMUUCUUAYRMYMUUCYNYHYPJIZMIZUUAYMUUHUU
      IUUGUUCUVFLUULUURUVDYNYPYHUQUPYMUVEYPYNMYMUVETYOKIZYPJIZYPYMYHUVGYPJYMYHG
      GHIZXAGHIZKIZUVGYMUUMUUTYHUVKLSUVAGXAURUKYMUVITUVJYOKUVITLYMUSUTYCYBUVJYO
      LWNWPURVGVAVBVCYMYPTGJIZYOKIZUVHUVLGYOKTGGTVDVEZSSVHVFVIYMUUNUVMUVHLZUUQT
      POUUMUUNUVOUVNSTGYOVJVKRVLVBVMVBVCVBYMYGYSYHMYCUUJUUOYGYSLYBUUKUUPWOWQVQU
      IVCYCUUJUUOYKUUBLYBUUKUUPWOWQVNUIVOWFVMYEXSYFYGKIZYFYHKIZMIZYJYEXQUVPXRUV
      QMYEYDWRPOZXQUVPLYBYCYDVPZYEUUJUUOUVSYCYBUUJYDUUKVRZYBYCUUOYDUUPVSZWOWQUL
      UMZWMWRURUMYEYDUUSXRUVQLUVTYEUUMUUTUUSSYBYCUUTYDUVAWFUVBUKZWMXBURUMVAYEYF
      POZYGPOZUUGYJUVRLYDYBUWEYCWMQVTYEUVSUWFUWCWRQRYEUUSUUGUWDUVCRYFYGYHWAUPWB
      YEYDXEPOZXTYLLUVTYEUUJUUOUWGUWAUWBWOWQWCUMWMXEURUMVOWDXHXKXSXLXTWTXDXKXSL
      XGWTXDXIXQXJXRMAWSGHWGBXCGHWGWEWFXGWTXLXTLXDCXFGHWGVTWHWIWJWKWL $.
      $( [19-Apr-2014] $) $( [28-Mar-2014] $)

    $( Lemma for ~ pythagtrip .  Prove the full version of one direction of the
       theorem.  (Contributed by Scott Fenton, 28-Mar-2014.) $)
    pythagtriplem2 $p |- ( ( A e. NN /\ B e. NN ) -> (
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ->
        ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) ) $=
      ( cn wcel wa cv c2 cexp co cmul wceq caddc wrex wo cvv cc cpr cmin w3a wb
      oprex preq12bg mpanr12 anbi1d andir df-3an orbi12i bitr4i syl6bb 2rexbidv
      rexbidv r19.43 2rexbii rexbii 3bitri wi pythagtriplem1 3ancoma sylbi nncn
      a1i sqcl syl addcom syl2an eqeq1d syl5ibr jaod sylbid ) AGHZBGHZIZABUADJZ
      EJZKLMZFJZKLMZUBMZNMZVQKVRVTNMNMZNMZUAOZCVQVSWAPMNMOZIZDGQZEGQFGQZAWCOZBW
      EOZWGUCZDGQZEGQZFGQZAWEOZBWCOZWGUCZDGQZEGQZFGQZRZAKLMZBKLMZPMZCKLMZOZVPWJ
      WMWSRZDGQZEGQFGQZXCVPWIXJFEGGVPWHXIDGVPWHWKWLIZWQWRIZRZWGIZXIVPWFXNWGVPWC
      SHWESHWFXNUDVQWBNUEVQWDNUEABWCWEGGSSUFUGUHXOXLWGIZXMWGIZRXIXLXMWGUIWMXPWS
      XQWKWLWGUJWQWRWGUJUKULUMUOUNXKWNWTRZEGQZFGQWOXARZFGQXCXJXRFEGGWMWSDGUPUQX
      SXTFGWNWTEGUPURWOXAFGUPUSUMVPWPXHXBWPXHUTVPABCDEFVAVEXBXHVPXEXDPMZXGOZXBW
      RWQWGUCZDGQZEGQFGQYBWTYDFEGGWSYCDGWQWRWGVBURUQBACDEFVAVCVPXFYAXGVNXDTHZXE
      THZXFYAOVOVNATHYEAVDAVFVGVOBTHYFBVDBVFVGXDXEVHVIVJVKVLVM $.
      $( [19-Apr-2014] $) $( [29-Mar-2014] $)

  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C ` and ` B ` are relatively prime
       under some conditions.  (Contributed by Scott Fenton, 8-Apr-2014.) $)
    pythagtriplem3 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( B gcd C ) = 1 ) $=
      ( cn wcel w3a c2 cexp co wceq cgcd c1 wbr wa nnz syl 3ad2ant1 syl2anc cc0
      cz caddc cdivides opreq2 adantl gcdadd gcdcom eqtr3d adantr simpl2 simpl3
      wn zsqcl 3ad2ant2 sqgcd simpl1 3eqtr4d 3adant3 simp3l opreq1d eqtrd cr wb
      cle cn0 3ad2ant3 gcdcl nn0re nn0ge0 1re lt01 ltleii sq11 mpanr12 mpbid
      0re ) ADEZBDEZCDEZFZAGHIZBGHIZUAIZCGHIZJZABKIZLJZGAUBMUKZNZFZBCKIZGHIZLGH
      IZJZWJLJZWIWKWEGHIZWLVSWDWKWOJWHVSWDNZWAWCKIZVTWAKIZWKWOWPWAWBKIZWQWRWDWS
      WQJVSWBWCWAKUCUDVSWSWRJWDVSWAVTKIZWSWRVSWATEZVTTEZWTWSJVQVPXAVRVQBTEZXABO
      ZBULPUMZVPVQXBVRVPATEXBAOAULPQZWAVTUERVSXAXBWTWRJXEXFWAVTUFRUGUHUGWPVQVRW
      KWQJVPVQVRWDUIZVPVQVRWDUJBCUNRWPVPVQWOWRJVPVQVRWDUOXGABUNRUPUQWIWELGHVSWD
      WFWGURUSUTWIWJVAEZSWJVCMZWMWNVBZVSWDXHWHVSWJVDEZXHVSXCCTEZXKVQVPXCVRXDUMV
      RVPXLVQCOVEBCVFRZWJVGPQVSWDXIWHVSXKXIXMWJVHPQXHXINLVAESLVCMXJVISLVOVIVJVK
      WJLVLVMRVN $.
      $( [19-Apr-2014] $) $( [8-Apr-2014] $)
  $}

  ${
    $( Lemma for ~ pythagtrip .  Show that ` C - B ` and ` C + B ` are
       relatively prime.  (Contributed by Scott Fenton, 12-Apr-2014.) $)
    pythagtriplem4 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( ( C - B ) gcd ( C + B ) ) = 1 ) $=
      ( cn wcel c2 co caddc wceq cgcd c1 cdivides wbr wa cmin cz syl2anc syl cc
      cmul w3a cexp wn simp3r nnz zsubcl syl2anr 3adant1 3ad2ant1 simp13 simp12
      wo nnaddcl gcddvds simprd breq1 biimpd mpan9 wi simpl13 simpl12 zaddcl 2z
      dvdsmultr1 mp3an1 nncn subsq breqtrrd simpl2 opreq1d simpl11 nnsqcl pncan
      mpd eqtr3d breqtrd adantr cprime 2prm prmdvdsexp mp3an13 mpbid mtand cneg
      wb 2nn 1nn0 nn0negzi gcdaddm pnncan 3anidm23 subcl opreq2d axaddcl negsub
      mulm1 eqtrd 2times adantl 3eqtr4d zmulcl sylancr eqbrtrd 1z ppncan mulid2
      3anidm13 cc0 wne nnne0 ancoms df-ne sylib intnand gcdn0cl dvdsgcd syl3anc
      syl21anc mp2and cn0 2nn0 mulgcd pythagtriplem3 2cn syl6eq dvdsprime orel1
      mulid1i sylc ) ADEZBDEZCDEZUAZAFUBGZBFUBGZHGZCFUBGZIZABJGKIZFALMZUCZNZUAZ
      CBOGZCBHGZJGZFIZUCUUGUUFKIZULZUUHUUCUUGYTYMYRYSUUAUDUUCUUGNZFYNLMZYTUUJFY
      QYOOGZYNLUUJFUUEUUDTGZUULLUUJFUUELMZFUUMLMZUUCUUFUUELMZUUGUUNUUCUUFUUDLMZ
      UUPUUCUUDPEZUUEPEZUUQUUPNYMYRUURUUBYKYLUURYJYLCPEZBPEZUURYKCUEZBUEZCBUFZU
      GUHUIZUUCUUEDEZUUSUUCYLYKUVFYJYKYLYRUUBUJZYJYKYLYRUUBUKZCBUMZQUUEUERZUUDU
      UEUNQUOUUGUUPUUNUUFFUUELUPUQURUUJUUSUURUUNUUOUSZUUJUUTUVAUUSUUJYLUUTYJYKY
      LYRUUBUUGUTZUVBRZUUJYKUVAYJYKYLYRUUBUUGVAZUVCRZCBVBQUUJUUTUVAUURUVMUVOUVD
      QFPEZUUSUURUVKVCFUUEUUDVDVEQVNUUJCSEZBSEZUULUUMIUUJYLUVQUVLCVFZRUUJYKUVRU
      VNBVFZRCBVGQVHUUJYPYOOGZUULYNUUJYPYQYOOYMYRUUBUUGVIVJUUJYNSEZYOSEZUWAYNIU
      UJYNDEZUWBUUJYJUWDYJYKYLYRUUBUUGVKAVLRYNVFRUUJYODEZUWCUUJYKUWEUVNBVLRYOVF
      RYNYOVMQVOVPUUJAPEZUUKYTWEZUUCUWFUUGYMYRUWFUUBYJYKUWFYLAUEUIUIVQFVREZUWFF
      DEUWGVSWFAFFVTWARWBWCUUCUUFFLMZUUIUUCUUFFBTGZFCTGZJGZFLUUCUUFUWJLMZUUFUWK
      LMZUUFUWLLMZUUCUUFUUDUWJJGZUWJLUUCUUFUUDUUEKWDZUUDTGZHGZJGZUWPUUCUURUUSUU
      FUWTIZUVEUVJUWQPEUURUUSUXAKWGWHUWQUUDUUEWIVEQUUCUVQUVRUWTUWPIUUCYLUVQUVGU
      VSRZUUCYKUVRUVHUVTRZUVQUVRNZUWSUWJUUDJUXDUUEUUDOGZBBHGZUWSUWJUVQUVRUXEUXF
      ICBBWJWKUXDUWSUUEUUDWDZHGZUXEUXDUWRUXGUUEHUXDUUDSEZUWRUXGICBWLZUUDWPRWMUX
      DUUESEUXIUXHUXEICBWNUXJUUEUUDWOQWQUVRUWJUXFIUVQBWRWSWTWMQWQUUCUWPUUDLMZUW
      PUWJLMZUUCUURUWJPEZUXKUXLNUVEUUCUVPUVAUXMVCUUCYKUVAUVHUVCRZFBXAXBZUUDUWJU
      NQUOXCUUCUUFUUDUWKJGZUWKLUUCUUFUUDUUEKUUDTGZHGZJGZUXPUUCUURUUSUUFUXSIZUVE
      UVJKPEUURUUSUXTXDKUUDUUEWIVEQUUCUXRUWKUUDJUUCUVQUVRUXRUWKIUXBUXCUXDUUEUUD
      HGZCCHGZUXRUWKUVQUVRUYAUYBICBCXEXGUXDUXQUUDUUEHUXDUXIUXQUUDIUXJUUDXFRWMUV
      QUWKUYBIUVRCWRVQWTQWMWQUUCUXPUUDLMZUXPUWKLMZUUCUURUWKPEZUYCUYDNUVEUUCUVPU
      UTUYEVCUUCYLUUTUVGUVBRZFCXAXBZUUDUWKUNQUOXCUUCUUFPEZUXMUYEUWMUWNNUWOUSUUC
      UUFDEZUYHUUCUURUUSUUDXHIZUUEXHIZNUCUYIUVEUVJUUCUYKUYJUUCUUEXHXIZUYKUCYMYR
      UYLUUBYKYLUYLYJYLYKUYLYLYKNUVFUYLUVIUUEXJRXKUHUIUUEXHXLXMXNUUDUUEXOXRZUUF
      UERUXOUYGUUFUWJUWKXPXQXSUUCUWLFBCJGZTGZFUUCUVAUUTUWLUYOIZUXNUYFFXTEUVAUUT
      UYPYAFBCYBVEQUUCUYOFKTGFUUCUYNKFTABCYCWMFYDYHYEWQVPUUCUWHUYIUWIUUIWEVSUYM
      FUUFYFXBWBUUGUUHYGYI $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  $( Lemma for ~ pythagtrip .  Show that ` C - B ` is positive.  (Contributed
     by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem10 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) ) -> 0 < ( C - B ) ) $=
    ( cn wcel c2 cexp co clt wbr cc0 cr 3ad2ant1 syl2anc wb resqcl syl 3ad2ant2
    nnre adantr w3a caddc wceq wa wne nnne0 sqgt0 ltaddpos2 mpbid simpr breqtrd
    cmin cle cn0 nnnn0 nn0ge0 3ad2ant3 lt2sq syl22anc mpbird posdif ) ADEZBDEZC
    DEZUAZAFGHZBFGHZUBHZCFGHZUCZUDZBCIJZKCBULHIJZVKVLVGVIIJZVKVGVHVIIVEVGVHIJZV
    JVEKVFIJZVOVEALEZAKUEZVPVBVCVQVDASMZVBVCVRVDAUFMAUGNVEVFLEZVGLEZVPVOOVEVQVT
    VSAPQVEBLEZWAVCVBWBVDBSRZBPQVFVGUHNUITVEVJUJUKVKWBKBUMJZCLEZKCUMJZVLVNOVEWB
    VJWCTZVEWDVJVCVBWDVDVCBUNEWDBUOBUPQRTVEWEVJVDVBWEVCCSUQTZVEWFVJVDVBWFVCVDCU
    NEWFCUOCUPQUQTBCURUSUTVKWBWEVLVMOWGWHBCVANUI $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C - B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem6 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C - B ) ) = ( ( C - B ) gcd A ) ) $=
    ( cn wcel w3a c2 cexp co wceq cgcd c1 cmin cz nnz syl2anc 3ad2ant1 eqtrd cc
    syl caddc cdivides wbr wn wa csqr cfv cn0 cmul cc0 3ad2ant3 3ad2ant2 zsubcl
    clt pythagtriplem10 3adant3 jca elnnz sylibr nnnn0 simp3 simp2 nnaddcl 3jca
    pythagtriplem4 opreq1d gcd12 zcn sqcl nncn pncan opreq1 eqtr3d subsq mulcom
    coprimeprodsq sylc fveq2d cr cle gcdcl nn0re nn0ge0 sqrsq ) ADEZBDEZCDEZFZA
    GHIZBGHIZUAIZCGHIZJZABKILJGAUBUCUDUEZFZCBMIZUFUGWPAKIZGHIZUFUGZWQWOWPWRUFWO
    WPUHEZCBUAIZNEZAUHEZFZWPXAKIZAKIZLJZUEWIWPXAUIIZJWPWRJWOXDXGWOWTXBXCWOWPDEZ
    WTWOWPNEZUJWPUNUCZUEXIWOXJXKWHWMXJWNWHCNEZBNEZXJWGWEXLWFCOUKWFWEXMWGBOULCBU
    MPZQZWHWMXKWNABCUOUPUQWPURUSWPUTTWHWMXBWNWHXADEZXBWHWGWFXPWEWFWGVAZWEWFWGVB
    ZCBVCPZXAOTQWHWMXCWNWEWFXCWGAUTQQVDWOXFLAKIZLWOXELAKABCVEVFWOANEZXTLJWHWMYA
    WNWEWFYAWGAOQZQZAVGTRUQWOWIWLWJMIZXHWOWKWJMIZWIYDWHWMYEWIJZWNWHWISEZWJSEZYF
    WHASEZYGWHYAYIYBAVHTAVITWHBSEZYHWHWFYJXRBVJTZBVITWIWJVKPQWMWHYEYDJWNWKWLWJM
    VLULVMWHWMYDXHJWNWHYDXAWPUIIZXHWHCSEZYJYDYLJWHWGYMXQCVJTYKCBVNPWHXASEZWPSEZ
    YLXHJWHXPYNXSXAVJTWHXJYOXNWPVHTXAWPVOPRQRWPXAAVPVQVRWOWQVSEZUJWQVTUCZWSWQJW
    OWQUHEZYPWOXJYAYRXOYCWPAWAPZWQWBTWOYRYQYSWQWCTWQWDPR $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Calculate ` ( sqr `` ( C + B ) ) ` .
     (Contributed by Scott Fenton, 18-Apr-2014.) $)
  pythagtriplem7 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       ( sqr ` ( C + B ) ) = ( ( C + B ) gcd A ) ) $=
    ( cn wcel c2 cexp co wceq cgcd c1 cmin cz nnz syl syl2anc 3ad2ant1 eqtrd cc
    nncn w3a caddc cdivides wbr wn csqr cfv cn0 cmul simp3 simp2 zsubcl nnaddcl
    wa nnnn0 3jca pythagtriplem4 opreq1d gcd12 jca pncan opreq1 3ad2ant2 eqtr3d
    sqcl subsq zcn mulcom coprimeprodsq2 sylc fveq2d cc0 cle gcdcl nn0re nn0ge0
    cr sqrsq ) ADEZBDEZCDEZUAZAFGHZBFGHZUBHZCFGHZIZABJHKIFAUCUDUEUNZUAZCBUBHZUF
    UGWJAJHZFGHZUFUGZWKWIWJWLUFWICBLHZMEZWJUHEZAUHEZUAZWNWJJHZAJHZKIZUNWCWNWJUI
    HZIWJWLIWIWRXAWIWOWPWQWBWGWOWHWBCMEZBMEZWOWBWAXCVSVTWAUJZCNOWBVTXDVSVTWAUKZ
    BNOCBULPZQWBWGWPWHWBWJDEZWPWBWAVTXHXEXFCBUMPZWJUOOQWBWGWQWHVSVTWQWAAUOQQUPW
    IWTKAJHZKWIWSKAJABCUQURWIAMEZXJKIWBWGXKWHVSVTXKWAANQQZAUSORUTWIWCWFWDLHZXBW
    IWEWDLHZWCXMWBWGXNWCIZWHWBWCSEZWDSEZXOWBASEZXPVSVTXRWAATQAVEOWBBSEZXQWBVTXS
    XFBTOZBVEOWCWDVAPQWGWBXNXMIWHWEWFWDLVBVCVDWBWGXMXBIWHWBXMWJWNUIHZXBWBCSEZXS
    XMYAIWBWAYBXECTOXTCBVFPWBWJSEZWNSEZYAXBIWBXHYCXIWJTOWBWOYDXGWNVGOWJWNVHPRQR
    WNWJAVIVJVKWIWKVQEZVLWKVMUDZWMWKIWIWKUHEZYEWIWJMEZXKYGWBWGYHWHWBXHYHXIWJNOQ
    XLWJAVNPZWKVOOWIYGYFYIWKVPOWKVRPR $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)


  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C - B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem8 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C - B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr cmin cfv pythagtriplem6 zsubcl syl2anr 3adant1 wne nnne0 df-ne sylib
    csqr intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIM
    KGANUAOPZFCBUBIZULUCVCALIZDABCUDUTVAVDDEZVBUTVCTEZATEZVCQKZAQKZPOZVEURUSVFU
    QUSCTEBTEVFURCRBRCBUEUFUGUQURVGUSARSUQURVJUSUQVIVHUQAQUHVIOAUIAQUJUKUMSVCAU
    NUOSUP $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Lemma for ~ pythagtrip .  Show that ` ( sqr `` ( C + B ) ) ` is a natural
     number (Contributed by Scott Fenton, 17-Apr-2014.) $)
  pythagtriplem9 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( sqr ` ( C + B ) ) e. NN ) $=
    ( cn wcel w3a c2 cexp co caddc wceq cgcd c1 cdivides wn wa cc0 nnz 3ad2ant1
    cz wbr csqr cfv pythagtriplem7 zaddcl syl2anr 3adant1 wne nnne0 df-ne sylib
    intnand gcdn0cl syl21anc eqeltrd ) ADEZBDEZCDEZFZAGHIBGHIJICGHIKZABLIMKGANU
    AOPZFZCBJIZUBUCVCALIZDABCUDVBVCTEZATEZVCQKZAQKZPOZVDDEUSUTVEVAUQURVEUPURCTE
    BTEVEUQCRBRCBUEUFUGSUSUTVFVAUPUQVFURARSSUSUTVIVAUPUQVIURUPVHVGUPAQUHVHOAUIA
    QUJUKULSSVCAUMUNUO $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)


  ${
    pythagtriplem11.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` M ` (which will eventually be
       closely related to the ` m ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem11 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> M e. NN ) $=
      ( cn wcel c2 co cdivides wbr wa cz cc0 clt nnz syl wb syl2anc 3ad2ant1 c1
      w3a cexp caddc wceq cgcd wn csqr cmin cdiv pythagtriplem9 simp3r 3ad2ant3
      cfv 3ad2ant2 zaddcl 2z dvdsgcdb mp3an1 simprd mtand pythagtriplem7 breq2d
      biimpar mtbird pythagtriplem8 zsubcl pythagtriplem6 opoe syl22anc nnaddcl
      wne 2ne0 divides2 mp3an12 mpbid nnre nngt0 addgt0 halfpos2 elnnz sylanbrc
      cr syl5eqel ) AFGZBFGZCFGZUBZAHUCIBHUCIUDICHUCIUEZABUFIUAUEZHAJKZUGZLZUBZ
      DCBUDIZUHUNZCBUIIZUHUNZUDIZHUJIZFEWNWTMGZNWTOKZWTFGWNHWSJKZXAWNWPMGZHWPJK
      ZUGWRMGZHWRJKZUGXCWNWPFGZXDABCUKZWPPQWNXEHWOAUFIZJKZWNXKWKWHWIWJWLULZWNXK
      LHWOJKZWKWNXMWKLZXKWNWOMGZAMGZXNXKRZWHWIXOWMWHCMGZBMGZXOWGWEXRWFCPUMZWFWE
      XSWGBPUOZCBUPSTWHWIXPWMWEWFXPWGAPTTZHMGZXOXPXQUQHWOAURUSSVDUTVAWNWPXJHJAB
      CVBVCVEWNWRFGZXFABCVFZWRPQWNXGHWQAUFIZJKZWNYGWKXLWNYGLHWQJKZWKWNYHWKLZYGW
      NWQMGZXPYIYGRZWHWIYJWMWHXRXSYJXTYACBVGSTYBYCYJXPYKUQHWQAURUSSVDUTVAWNWRYF
      HJABCVHVCVEWPWRVIVJWNWSMGZXCXARZWNWSFGZYLWNXHYDYNXIYEWPWRVKSZWSPQYCHNVLYL
      YMUQVMHWSVNVOQVPWNNWSOKZXBWNWPWCGZWRWCGZNWPOKZNWROKZYPWNXHYQXIWPVQQWNYDYR
      YEWRVQQWNXHYSXIWPVRQWNYDYTYEWRVRQWPWRVSVJWNWSWCGZYPXBRWNYNUUAYOWSVQQWSVTQ
      VPWTWAWBWD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` M ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem12 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( M ^ 2 ) = ( ( C + A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cdiv cmul syl syl2anc 3ad2ant1 cc0 2cn
      cc cn w3a cgcd c1 cdivides wn wa csqr cfv opreq1i axaddcl syl2anr 3adant1
      cmin nncn sqrcl subcl addcl wne 2ne0 mp3an23 sqvali opreq2i syl6eq binom2
      sqdiv cr cle nnre axaddrcl clt 3ad2ant3 3ad2ant2 nngt0 addgt0 syl22anc wi
      0re ltle mpan sylc sqsqr opreq1d resubcl pythagtriplem10 3adant3 opreq12d
      ppncan syl3anc 2times eqtr4d opreq1 sqcl pncan 3eqtr3rd fveq2d sqrmul cn0
      subsq nnnn0 nn0ge0 sqrsq 3eqtr3d opreq2d mulcl sylancr add32 adddi mp3an1
      3eqtr4d 3eqtrd 3adant2 pm3.2i divdiv1 divcan3 3eqtr2d eqtrd syl5eq ) AUAF
      ZBUAFZCUAFZUBZAGHIZBGHIZJIZCGHIZKZABUCIUDKGAUELUFUGZUBZDGHICBJIZUHUIZCBUN
      IZUHUIZJIZGMIZGHIZCAJIZGMIZDYOGHEUJYIYPYNGHIZGGNIZMIZYRYIYNTFZYPUUAKYBYGU
      UBYHYBYKTFZYMTFZUUBYBYJTFZUUCXTYAUUEXSYACTFZBTFZUUEXTCUOZBUOZCBUKULUMZYJU
      POZYBYLTFZUUDXTYAUULXSYAUUFUUGUULXTUUHUUICBUQULUMZYLUPOZYKYMURPQUUBYPYSGG
      HIZMIZUUAUUBGTFZGRUSZYPUUPKSUTYNGVFVAUUOYTYSMGSVBVCVDOYIUUAGYQNIZYTMIZUUS
      GMIZGMIZYRYIYSUUSYTMYIYSYKGHIZGYKYMNIZNIZJIZYMGHIZJIZYJUVEJIZYLJIZUUSYIUU
      CUUDYSUVHKYBYGUUCYHUUKQYBYGUUDYHUUNQYKYMVEPYIUVFUVIUVGYLJYIUVCYJUVEJYIYJV
      GFZRYJVHLZUVCYJKYBYGUVKYHXTYAUVKXSYACVGFZBVGFZUVKXTCVIZBVIZCBVJULUMQZYIUV
      KRYJVKLZUVLUVQYBYGUVRYHYBUVMUVNRCVKLZRBVKLZUVRYAXSUVMXTUVOVLXTXSUVNYAUVPV
      MYAXSUVSXTCVNVLXTXSUVTYABVNVMCBVOVPQRVGFZUVKUVRUVLVQVRRYJVSVTWAZYJWBPWCYI
      YLVGFZRYLVHLZUVGYLKYBYGUWCYHXTYAUWCXSYAUVMUVNUWCXTUVOUVPCBWDULUMQZYIUWCRY
      LVKLZUWDUWEYBYGUWFYHABCWEWFUWAUWCUWFUWDVQVRRYLVSVTWAZYLWBPWGYIYJYLJIZUVEJ
      IZGCNIZGANIZJIZUVJUUSYIUWHUWJUVEUWKJYIUWHCCJIZUWJYIUUFUUGUUFUWHUWMKYBYGUU
      FYHYAXSUUFXTUUHVLQZYBYGUUGYHXTXSUUGYAUUIVMQZUWNCBCWHWIYIUUFUWJUWMKUWNCWJO
      WKYIUVDAGNYIYJYLNIZUHUIZYCUHUIZUVDAYIUWPYCUHYIYEYDUNIZYFYDUNIZYCUWPYGYBUW
      SUWTKYHYEYFYDUNWLVMYIYCTFZYDTFZUWSYCKYIATFZUXAYBYGUXCYHXSXTUXCYAAUOZQQZAW
      MOYIUUGUXBUWOBWMOYCYDWNPYIUUFUUGUWTUWPKUWNUWOCBWSPWOWPYIUVKUVLUWCUWDUWQUV
      DKUVQUWBUWEUWGYJYLWQVPYIAVGFZRAVHLZUWRAKYBYGUXFYHXSXTUXFYAAVIQQYBYGUXGYHX
      SXTUXGYAXSAWRFUXGAWTAXAOQQAXBPXCXDWGYIUUEUVETFZUULUVJUWIKYBYGUUEYHUUJQYBY
      GUXHYHYBUUQUVDTFZUXHSYBUUCUUDUXIUUKUUNYKYMXEPGUVDXEXFQYBYGUULYHUUMQYJUVEY
      LXGWIYIUUFUXCUUSUWLKZUWNUXEUUQUUFUXCUXJSGCAXHXIPXJXKWCYIUUSTFZUVBUUTKZYIU
      UQYQTFZUXKSYBYGUXMYHXSYAUXMXTYAUUFUXCUXMXSUUHUXDCAURULXLQZGYQXEXFUXKUUQUU
      RUGZUXOUXLUUQUURSUTXMZUXPUUSGGXNVAOYIUVAYQGMYIUXMUVAYQKZUXNUXMUUQUURUXQSU
      TYQGXOVAOWCXPXQXR $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    pythagtriplem13.1 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show that ` N ` (which will eventually be
       closely related to the ` n ` in the final statement) is a natural.
       (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem13 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> N e. NN ) $=
      ( cn wcel c2 co cdivides wbr cz cc0 clt nnz syl wb syl2anc 3ad2ant1 cr c1
      w3a cexp caddc wceq cgcd wn wa csqr cmin cdiv pythagtriplem9 simp3r simp3
      cfv simp2 nnaddcl 2z dvdsgcdb mp3an1 biimpar simprd pythagtriplem7 breq2d
      mtand mtbird pythagtriplem8 3ad2ant3 3ad2ant2 pythagtriplem6 syl22anc zre
      zsubcl omoe simp13 nnre crp ltsubrp nngt0 simp12 ltaddpos mpbid lttrd cle
      nnrp pythagtriplem10 3adant3 wi 0re ltle mpan sylc addgt0 sqrlt nnsub wne
      2ne0 divides2 mp3an12 halfpos2 elnnz sylanbrc syl5eqel ) AFGZBFGZCFGZUBZA
      HUCIBHUCIUDICHUCIUEZABUFIUAUEZHAJKZUGZUHZUBZDCBUDIZUIUOZCBUJIZUIUOZUJIZHU
      KIZFEXMXSLGZMXSNKZXSFGXMHXRJKZXTXMXOLGZHXOJKZUGXQLGZHXQJKZUGYBXMXOFGZYCAB
      CULZXOOPXMYDHXNAUFIZJKZXMYJXJXGXHXIXKUMZXMYJUHHXNJKZXJXMYLXJUHZYJXMXNLGZA
      LGZYMYJQZXGXHYNXLXGXNFGZYNXGXFXEYQXDXEXFUNXDXEXFUPCBUQRZXNOPSXGXHYOXLXDXE
      YOXFAOSSZHLGZYNYOYPURHXNAUSUTRVAVBVEXMXOYIHJABCVCVDVFXMXQFGZYEABCVGZXQOPX
      MYFHXPAUFIZJKZXMUUDXJYKXMUUDUHHXPJKZXJXMUUEXJUHZUUDXMXPLGZYOUUFUUDQZXGXHU
      UGXLXGCLGZBLGZUUGXFXDUUIXECOVHXEXDUUJXFBOVICBVMRZSYSYTUUGYOUUHURHXPAUSUTR
      VAVBVEXMXQUUCHJABCVJVDVFXOXQVNVKXMXRLGZYBXTQZXMXRFGZUULXMXQXONKZUUNXMXPXN
      NKZUUOXMXPCXNXGXHXPTGZXLXGUUGUUQUUKXPVLPSZXMXFCTGZXDXEXFXHXLVOCVPPZXGXHXN
      TGZXLXGYQUVAYRXNVPPSZXMUUSBVQGZXPCNKUUTXGXHUVCXLXEXDUVCXFBWEVISCBVRRXMMBN
      KZCXNNKZXGXHUVDXLXEXDUVDXFBVSVISZXMBTGZUUSUVDUVEQXMXEUVGXDXEXFXHXLVTBVPPZ
      UUTBCWARWBWCXMUUQMXPWDKZUVAMXNWDKZUUPUUOQUURXMUUQMXPNKZUVIUURXGXHUVKXLABC
      WFWGMTGZUUQUVKUVIWHWIMXPWJWKWLUVBXMUVAMXNNKZUVJUVBXMUUSUVGMCNKZUVDUVMUUTU
      VHXGXHUVNXLXFXDUVNXECVSVHSUVFCBWMVKUVLUVAUVMUVJWHWIMXNWJWKWLXPXNWNVKWBXMU
      UAYGUUOUUNQUUBYHXQXOWORWBZXROPYTHMWPUULUUMURWQHXRWRWSPWBXMMXRNKZYAXMUUNUV
      PUVOXRVSPXMXRTGZUVPYAQXMUUNUVQUVOXRVPPXRWTPWBXSXAXBXC $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Calculate the square of ` N ` .  (Contributed
       by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem14 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> ( N ^ 2 ) = ( ( C - A ) / 2 ) ) $=
      ( wcel c2 cexp co caddc wceq wbr cmin cdiv syl syl2anc 3ad2ant1 cc0 cmul
      cc cn w3a cgcd c1 cdivides wn csqr opreq1i nncn addcl syl2anr sqrcl subcl
      cfv 3adant1 wne 2cn 2ne0 sqdiv mp3an23 sqvali opreq2i sqcl pm3.2i divdiv1
      wa simp12 simp13 binom2sub cr cle axaddrcl clt adantl adantr nngt0 addgt0
      nnre syl22anc wi 0re ltle mpan sylc sqsqr opreq1d resubcl pythagtriplem10
      3adant3 opreq12d simp11 subdi mp3an1 ppncan 3anidm13 2times eqtr4d opreq1
      3ad2ant2 pncan subsq fveq2d sqrmul cn0 nnnn0 nn0ge0 sqrsq 3eqtr3d opreq2d
      3eqtr3rd recnd mulcl sylancr addsub syl3anc 3eqtr2rd 3eqtrd 3adant2 eqtrd
      divcan3 eqtr3d syl5eq ) AUAFZBUAFZCUAFZUBZAGHIZBGHIZJIZCGHIZKZABUCIUDKGAU
      ELUFVFZUBZDGHICBJIZUGUNZCBMIZUGUNZMIZGNIZGHIZCAMIZGNIZDYSGHEUHYMYTYRGHIZG
      GHIZNIZUUBYMYRTFZYTUUEKZYFYKUUFYLYDYEUUFYCYDYEVFZYOTFZYQTFZUUFUUHYNTFZUUI
      YECTFZBTFZUUKYDCUIZBUIZCBUJUKYNULOZUUHYPTFZUUJYEUULUUMUUQYDUUNUUOCBUMUKYP
      ULOZYOYQUMPUOQZUUFGTFZGRUPZUUGUQURYRGUSUTOYMUUEUUCGGSIZNIZUUBUUDUVBUUCNGU
      QVAVBYMUUCGNIZGNIZUVCUUBYMUUCTFZUVEUVCKZYMUUFUVFUUSYRVCOUVFUUTUVAVFZUVHUV
      GUUTUVAUQURVDZUVIUUCGGVEUTOYMUVDUUAGNYMUVDGUUASIZGNIZUUAYMUUCUVJGNYMUUCYO
      GHIZGYOYQSIZSIZMIZYQGHIZJIZYNUVNMIZYPJIZUVJYMUUIUUJUUCUVQKYMYDYEUUIYCYDYE
      YKYLVGZYCYDYEYKYLVHZUUPPYMYDYEUUJUVTUWAUURPYOYQVIPYMUVOUVRUVPYPJYMUVLYNUV
      NMYMYNVJFZRYNVKLZUVLYNKYFYKUWBYLYDYEUWBYCYECVJFZBVJFZUWBYDCVRZBVRZCBVLUKZ
      UOQZYFYKUWCYLYDYEUWCYCUUHUWBRYNVMLZUWCUWHUUHUWDUWERCVMLZRBVMLZUWJYEUWDYDU
      WFVNYDUWEYEUWGVOYEUWKYDCVPVNYDUWLYEBVPVOCBVQVSRVJFZUWBUWJUWCVTWARYNWBWCWD
      UOQZYNWEPWFYMYPVJFZRYPVKLZUVPYPKYFYKUWOYLYDYEUWOYCYEUWDUWEUWOYDUWFUWGCBWG
      UKUOQZYMUWORYPVMLZUWPUWQYFYKUWRYLABCWHWIUWMUWOUWRUWPVTWARYPWBWCWDZYPWEPWJ
      YMUVJGCSIZGASIZMIZYNYPJIZUVNMIZUVSYMUULATFZUVJUXBKZYMYEUULUWAUUNOZYMYCUXE
      YCYDYEYKYLWKAUIZOUUTUULUXEUXFUQGCAWLWMPYMUXCUWTUVNUXAMYFYKUXCUWTKZYLYDYEU
      XIYCYEUULUUMUXIYDUUNUUOUULUUMVFUXCCCJIZUWTUULUUMUXCUXJKCBCWNWOUULUWTUXJKU
      UMCWPVOWQUKUOQYMUVMAGSYMYNYPSIZUGUNZYGUGUNZUVMAYMUXKYGUGYMYIYHMIZYJYHMIZY
      GUXKYKYFUXNUXOKYLYIYJYHMWRWSYFYKUXNYGKZYLYFYGTFZYHTFZUXPYCYDUXQYEYCUXEUXQ
      UXHAVCOQYDYCUXRYEYDUUMUXRUUOBVCOWSYGYHWTPQYMUULUUMUXOUXKKUXGYMYDUUMUVTUUO
      OCBXAPXJXBYMUWBUWCUWOUWPUXLUVMKUWIUWNUWQUWSYNYPXCVSYMAVJFZRAVKLZUXMAKYFYK
      UXSYLYCYDUXSYEAVRQQYFYKUXTYLYCYDUXTYEYCAXDFUXTAXEAXFOQQAXGPXHXIWJYMUUKUUQ
      UVNTFZUXDUVSKYMYNUWIXKYMYPUWQXKYFYKUYAYLYFUUTUVMTFZUYAUQYFUUIUUJUYBYDYEUU
      IYCUUPUOYDYEUUJYCUURUOYOYQXLPGUVMXLXMQYNYPUVNXNXOXPXQWFYMUUATFZUVKUUAKZYF
      YKUYCYLYCYEUYCYDYEUULUXEUYCYCUUNUXHCAUMUKXRQUYCUUTUVAUYDUQURUUAGXTUTOXSWF
      YAYBXSYB $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)
  $}

  ${
    pythagtriplem15.1 $e |- M = ( ( ( sqr ` ( C + B ) ) + ( sqr ` ( C - B ) ) )
        / 2 ) $.
    pythagtriplem15.2 $e |- N = ( ( ( sqr ` ( C + B ) ) - ( sqr ` ( C - B ) ) )
        / 2 ) $.
    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` A ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem15 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> A = ( ( M ^ 2 ) - ( N ^ 2 ) ) ) $=
      ( cn wcel c2 cexp co caddc wceq cmin cdiv cc syl2anc syl 3ad2ant1 cgcd c1
      w3a cdivides wn wa pythagtriplem12 pythagtriplem14 opreq12d simp3 nnaddcl
      wbr simp1 nncn cz nnz 3ad2ant3 zsubcl zcn cc0 wne pm3.2i divsubdir mp3an3
      2cn 2ne0 cmul pnncan syl3anc 2times eqtr4d opreq1d divcan3 mp3an23 eqtr3d
      eqtrd eqtr2d ) AHIZBHIZCHIZUCZAJKLBJKLMLCJKLNZABUALUBNJAUDULUEUFZUCZDJKLZ
      EJKLZOLCAMLZJPLZCAOLZJPLZOLZAWDWEWHWFWJOABCDFUGABCEGUHUIWDWGWIOLZJPLZWKAW
      DWGQIZWIQIZWMWKNZWAWBWNWCWAWGHIZWNWAVTVRWQVRVSVTUJVRVSVTUMCAUKRWGUNSTWAWB
      WOWCWAWIUOIZWOWACUOIZAUOIZWRVTVRWSVSCUPUQVRVSWTVTAUPTCAURRWIUSSTWNWOJQIZJ
      UTVAZUFWPXAXBVEVFVBWGWIJVCVDRWDWMJAVGLZJPLZAWDWLXCJPWDWLAAMLZXCWDCQIZAQIZ
      XGWLXENWAWBXFWCVTVRXFVSCUNUQTWAWBXGWCVRVSXGVTAUNTTZXHCAAVHVIWDXGXCXENXHAV
      JSVKVLWDXGXDANZXHXGXAXBXIVEVFAJVMVNSVPVOVQ $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` B ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem16 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> B = ( 2 x. ( M x. N ) ) ) $=
      ( wcel c2 co wceq wbr cmul cdiv cc syl syl2anc 3adant1 3ad2ant1 cc0 caddc
      cn w3a cexp cgcd c1 cdivides wn csqr cfv cmin opreq12i nncn addcl syl2anr
      wa sqrcl subcl wne 2ne0 pm3.2i divmuldiv mpanr12 mulcl divdiv1 mp3an23 cr
      2cn cle nnre axaddrcl clt adantl adantr nngt0 addgt0 syl22anc wi 0re ltle
      mpan sqsqr resubcl pythagtriplem10 3adant3 opreq12d opreq1d simp12 simp13
      subsq pnncan 3anidm23 2times eqtr4d divcan3 eqtrd 3eqtr3d 3eqtr2d opreq2d
      sylc syl5eq divcan2 3ad2ant2 eqtr2d ) AUBHZBUBHZCUBHZUCZAIUDJBIUDJUAJCIUD
      JKZABUEJUFKIAUGLUHUPZUCZIDEMJZMJIBINJZMJZBXKXLXMIMXKXLCBUAJZUIUJZCBUKJZUI
      UJZUAJZINJZXPXRUKJZINJZMJZXMDXTEYBMFGULXKYCXSYAMJZIIMJNJZYDINJZINJZXMXKXS
      OHZYAOHZYCYEKZXHXIYHXJXFXGYHXEXFXGUPZXPOHZXROHZYHYKXOOHZYLXGCOHZBOHZYNXFC
      UMZBUMZCBUNUOXOUQPZYKXQOHZYMXGYOYPYTXFYQYRCBURUOXQUQPZXPXRUNQZRSXHXIYIXJX
      FXGYIXEYKYLYMYIYSUUAXPXRURQZRSYHYIUPIOHZITUSZUPZUUFYJUUDUUEVHUTVAZUUGXSYA
      IIVBVCQXKYDOHZYGYEKZXHXIUUHXJXFXGUUHXEYKYHYIUUHUUBUUCXSYAVDQRSUUHUUFUUFUU
      IUUGUUGYDIIVEVFPXKYFBINXKXPIUDJZXRIUDJZUKJZINJXOXQUKJZINJZYFBXKUULUUMINXK
      UUJXOUUKXQUKXKXOVGHZTXOVILZUUJXOKXHXIUUOXJXFXGUUOXEXGCVGHZBVGHZUUOXFCVJZB
      VJZCBVKUOZRSXHXIUUPXJXFXGUUPXEYKUUOTXOVLLZUUPUVAYKUUQUURTCVLLZTBVLLZUVBXG
      UUQXFUUSVMXFUURXGUUTVNXGUVCXFCVOVMXFUVDXGBVOVNCBVPVQTVGHZUUOUVBUUPVRVSTXO
      VTWAWTRSXOWBQXKXQVGHZTXQVILZUUKXQKXHXIUVFXJXFXGUVFXEXGUUQUURUVFXFUUSUUTCB
      WCUORSZXKUVFTXQVLLZUVGUVHXHXIUVIXJABCWDWEUVEUVFUVIUVGVRVSTXQVTWAWTXQWBQWF
      WGXKUULYDINXKYLYMUULYDKXKXFXGYLXEXFXGXIXJWHZXEXFXGXIXJWIZYSQXKXFXGYMUVJUV
      KUUAQXPXRWJQWGXKUUNIBMJZINJZBXKUUMUVLINXHXIUUMUVLKZXJXFXGUVNXEXGYOYPUVNXF
      YQYRYOYPUPUUMBBUAJZUVLYOYPUUMUVOKCBBWKWLYPUVLUVOKYOBWMVMWNUORSWGXKYPUVMBK
      ZXKXFYPUVJYRPYPUUDUUEUVPVHUTBIWOVFPWPWQWGWRXAWSXHXIXNBKZXJXFXEUVQXGXFYPUV
      QYRYPUUDUUEUVQVHUTBIXBVFPXCSXD $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

    $( Lemma for ~ pythagtrip .  Show the relationship between ` M ` , ` N ` ,
       and ` C ` .  (Contributed by Scott Fenton, 17-Apr-2014.) $)
    pythagtriplem17 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) -> C = ( ( M ^ 2 ) + ( N ^ 2 ) ) ) $=
      ( cn wcel w3a c2 cexp co caddc wceq wa cdiv cc 3ad2ant1 syl2anc c1 wbr wn
      cgcd cdivides cmin pythagtriplem12 pythagtriplem14 opreq12d nncn 3ad2ant3
      addcl subcl cc0 wne 2cn 2ne0 pm3.2i divdir mp3an3 cmul ppncan syl3anc syl
      2times eqtr4d opreq1d divcan3 mp3an23 eqtrd eqtr3d eqtr2d ) AHIZBHIZCHIZJ
      ZAKLMBKLMNMCKLMOZABUDMUAOKAUEUBUCPZJZDKLMZEKLMZNMCANMZKQMZCAUFMZKQMZNMZCV
      SVTWCWAWENABCDFUGABCEGUHUIVSWBWDNMZKQMZWFCVSWBRIZWDRIZWHWFOZVSCRIZARIZWIV
      PVQWLVRVOVMWLVNCUJUKSZVPVQWMVRVMVNWMVOAUJSSZCAULTVSWLWMWJWNWOCAUMTWIWJKRI
      ZKUNUOZPWKWPWQUPUQURWBWDKUSUTTVSWHKCVAMZKQMZCVSWGWRKQVSWGCCNMZWRVSWLWMWLW
      GWTOWNWOWNCACVBVCVSWLWRWTOWNCVEVDVFVGVSWLWSCOZWNWLWPWQXAUPUQCKVHVIVDVJVKV
      L $.
      $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $}

  ${
    $d A m n $.  $d B m n $.  $d C m n $.
    $( Lemma for ~ pythagtrip .  Wrap the previous ` M ` and ` N ` up in
       quanitifers.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem18 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       ( ( A gcd B ) = 1 /\ -. 2 || A ) ) ->
       E. n e. NN E. m e. NN
       ( A = ( ( m ^ 2 ) - ( n ^ 2 ) ) /\
         B = ( 2 x. ( m x. n ) ) /\
         C = ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) $=
      ( cn wcel w3a c2 cexp caddc wceq csqr cfv cmin cmul opreq1 opreq2d eqeq2d
      co cgcd c1 cdivides wbr wn cdiv wrex eqid pythagtriplem13 pythagtriplem11
      pythagtriplem15 pythagtriplem16 pythagtriplem17 opreq2 3anbi123d rcla42ev
      wa cv opreq1d syl113anc ) AFGBFGCFGHAIJTBIJTKTCIJTLABUATUBLIAUCUDUEUQHCBK
      TMNZCBOTMNZOTIUFTZFGVAVBKTIUFTZFGAVDIJTZVCIJTZOTZLZBIVDVCPTZPTZLZCVEVFKTZ
      LZADURZIJTZEURZIJTZOTZLZBIVNVPPTZPTZLZCVOVQKTZLZHZDFUGEFUGABCVCVCUHZUIABC
      VDVDUHZUJABCVDVCWGWFUKABCVDVCWGWFULABCVDVCWGWFUMWEVHVKVMHAVOVFOTZLZBIVNVC
      PTZPTZLZCVOVFKTZLZHEDVCVDFFVPVCLZVSWIWBWLWDWNWOVRWHAWOVQVFVOOVPVCIJQZRSWO
      WAWKBWOVTWJIPVPVCVNPUNRSWOWCWMCWOVQVFVOKWPRSUOVNVDLZWIVHWLVKWNVMWQWHVGAWQ
      VOVEVFOVNVDIJQZUSSWQWKVJBWQWJVIIPVNVDVCPQRSWQWMVLCWQVOVEVFKWRUSSUOUPUT $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A m n k $.  $d B m n k $.  $d C m n k $.
    $( Lemma for ~ pythagtrip .  Introduce ` k ` and remove the relative
       primality requirement.  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    pythagtriplem19 $p |- ( ( ( A e. NN /\ B e. NN /\ C e. NN ) /\
       ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) /\
       -. 2 || ( A / ( A gcd B ) ) ) ->
       E. n e. NN E. m e. NN E. k e. NN
       ( A = ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) /\
         B = ( k x. ( 2 x. ( m x. n ) ) ) /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) $=
      ( cn wcel c2 cexp co wceq wbr cmul wrex cz cc0 3ad2ant1 syl syl3anc caddc
      w3a cgcd cdiv cdivides wn cv cmin wa nnz anim12i wne nnne0 df-ne intnanrd
      sylib adantr gcdn0cl syl2anc 3adant3 c1 gcddvds syl2an simpld wb divides2
      mpbid cr nnre nngt0 divgt0 syl22anc elnnz sylanbrc simprd 3ad2ant2 dvdssq
      clt anbi12d nnsqcl dvds2add mpd simpr breqtrd 3ad2ant3 mpbird opreq1 nncn
      wi cc sqdiv opreq12d divdir syl112anc eqtr4d gcddiv syl31anc divid eqtr3d
      3eqtr4d simp3 pythagtriplem18 syl312anc divcan2 eqcomd 3jca opreq2 eqeq2d
      3anbi123d syl5ibcom reximdv 2rexbidv rcla4ev rexcom rexbii bitri ) AGHZBG
      HZCGHZUBZAIJKZBIJKZUAKZCIJKZLZIAABUCKZUDKZUEMUFZUBZADUGZEUGZIJKZFUGZIJKZU
      HKZNKZLZBYJIYKYMNKNKZNKZLZCYJYLYNUAKZNKZLZUBZEGOZFGOZDGOZUUDDGOEGOZFGOZYI
      YFGHZAYFYONKZLZBYFYRNKZLZCYFUUANKZLZUBZEGOZFGOZUUGXTYEUUJYHXQXRUUJXSXQXRU
      IAPHZBPHZUIAQLZBQLZUIUFZUUJXQUUTXRUVAAUJZBUJZUKXQUVDXRXQUVBUVCXQAQULUVBUF
      AUMAQUNUPUOUQABURUSUTZRYIYGYOLZBYFUDKZYRLZCYFUDKZUUALZUBZEGOZFGOZUUSYIYGG
      HZUVIGHZUVKGHZYGIJKZUVIIJKZUAKZUVKIJKZLYGUVIUCKZVALZYHUVOXTYEUVPYHXTYGPHZ
      QYGVRMZUVPXTYFAUEMZUWEXTUWGYFBUEMZXQXRUWGUWHUIZXSXQUUTUVAUWIXRUVEUVFABVBV
      CUTZVDXTYFPHZYFQULZUUTUWGUWEVEXTUUJUWKUVGYFUJSZXTUUJUWLUVGYFUMSZXQXRUUTXS
      UVERZYFAVFTVGXTAVHHZQAVRMZYFVHHZQYFVRMZUWFXQXRUWPXSAVIRXQXRUWQXSAVJRXTUUJ
      UWRUVGYFVISZXTUUJUWSUVGYFVJSZAYFVKVLYGVMVNRXTYEUVQYHXTUVIPHZQUVIVRMZUVQXT
      UWHUXBXTUWGUWHUWJVOXTUWKUWLUVAUWHUXBVEUWMUWNXRXQUVAXSUVFVPZYFBVFTVGXTBVHH
      ZQBVRMZUWRUWSUXCXRXQUXEXSBVIVPXRXQUXFXSBVJVPUWTUXABYFVKVLUVIVMVNRXTYEUVRY
      HXTYEUIZUVKPHZQUVKVRMZUVRUXGYFCUEMZUXHUXGUXJYFIJKZYDUEMZUXGUXKYCYDUEXTUXK
      YCUEMZYEXTUXKYAUEMZUXKYBUEMZUIZUXMXTUWIUXPUWJXTUWGUXNUWHUXOXTUWKUUTUWGUXN
      VEUWMUWOYFAVQUSXTUWKUVAUWHUXOVEUWMUXDYFBVQUSVSVGXTUXKPHZYAPHZYBPHZUXPUXMW
      IXTUXKGHZUXQXTUUJUXTUVGYFVTSZUXKUJSXTYAGHZUXRXQXRUYBXSAVTRZYAUJSXTYBGHZUX
      SXRXQUYDXSBVTVPZYBUJSUXKYAYBWATWBUQXTYEWCWDXTUXJUXLVEZYEXTUWKCPHZUYFUWMXS
      XQUYGXRCUJWEZYFCVQUSUQWFXTUXJUXHVEZYEXTUWKUWLUYGUYIUWMUWNUYHYFCVFTUQVGXTU
      XIYEXTCVHHZQCVRMZUWRUWSUXIXSXQUYJXRCVIWEXSXQUYKXRCVJWEUWTUXACYFVKVLUQUVKV
      MVNUTYIYCUXKUDKZYDUXKUDKZUWAUWBYEXTUYLUYMLYHYCYDUXKUDWGVPYIUWAYAUXKUDKZYB
      UXKUDKZUAKZUYLXTYEUWAUYPLYHXTUVSUYNUVTUYOUAXTAWJHZYFWJHZUWLUVSUYNLXQXRUYQ
      XSAWHRZXTUUJUYRUVGYFWHSZUWNAYFWKTXTBWJHZUYRUWLUVTUYOLXRXQVUAXSBWHVPZUYTUW
      NBYFWKTWLRXTYEUYLUYPLZYHXTYAWJHZYBWJHZUXKWJHZUXKQULZVUCXTUYBVUDUYCYAWHSXT
      UYDVUEUYEYBWHSXTUXTVUFUYAUXKWHSXTUXTVUGUYAUXKUMSYAYBUXKWMWNRWOXTYEUWBUYML
      ZYHXTCWJHZUYRUWLVUHXSXQVUIXRCWHWEZUYTUWNCYFWKTRWTXTYEUWDYHXTYFYFUDKZUWCVA
      XTUUTUVAUUJUWIVUKUWCLUWOUXDUVGUWJABYFWPWQXTUYRUWLVUKVALUYTUWNYFWRUSWSRXTY
      EYHXAYGUVIUVKEFXBXCYIUVNUURFGYIUVMUUQEGYIAYFYGNKZLZBYFUVINKZLZCYFUVKNKZLZ
      UBZUVMUUQXTYEVURYHXTVUMVUOVUQXTVULAXTUYQUYRUWLVULALUYSUYTUWNAYFXDTXEXTVUN
      BXTVUAUYRUWLVUNBLVUBUYTUWNBYFXDTXEXTVUPCXTVUIUYRUWLVUPCLVUJUYTUWNCYFXDTXE
      XFRUVMVUMUULVUOUUNVUQUUPUVHUVJVUMUULVEUVLUVHVULUUKAYGYOYFNXGXHRUVJUVHVUOU
      UNVEUVLUVJVUNUUMBUVIYRYFNXGXHVPUVLUVHVUQUUPVEUVJUVLVUPUUOCUVKUUAYFNXGXHWE
      XIXJXKXKWBUUFUUSDYFGYJYFLZUUDUUQFEGGVUSYQUULYTUUNUUCUUPVUSYPUUKAYJYFYONWG
      XHVUSYSUUMBYJYFYRNWGXHVUSUUBUUOCYJYFUUANWGXHXIXLXMUSUUGUUEDGOZFGOUUIUUEDF
      GGXNVUTUUHFGUUDDEGGXNXOXPUP $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d A k m n $.  $d B k m n $.  $d C k m n $.
    $( Parameterize the Pythagorean triples.  If ` A ` , ` B ` , and ` C ` are
       naturals, then they obey the Pythagorean triple formula iff they are
       parameterized by three naturals.  This proof follows the Isabelle proof
       at ~ http://afp.sourceforge.net/entries/Fermat3_4.shtml .  (Contributed
       by Scott Fenton, 19-Apr-2014.) $)
    pythagtrip $p |- ( ( A e. NN /\ B e. NN /\ C e. NN ) ->
    ( ( ( A ^ 2 ) + ( B ^ 2 ) ) = ( C ^ 2 ) <->
       E. n e. NN E. m e. NN E. k e. NN
       ( { A , B } = { ( k x. ( ( m ^ 2 ) - ( n ^ 2 ) ) ) ,
                       ( k x. ( 2 x. ( m x. n ) ) ) } /\
         C = ( k x. ( ( m ^ 2 ) + ( n ^ 2 ) ) ) ) ) ) $=
      ( cn wcel w3a c2 cexp co wceq cmul wa wrex wo cdiv cdivides 3adant3 caddc
      cpr cv cmin cgcd wn divgcdodd adantr pythagtriplem19 3expia simp12 simp11
      wbr simp13 nnsqcl nncn syl 3ad2ant1 3ad2ant2 addcom syl2anc eqeq1d biimpa
      cc cz nnz gcdcom opreq2d breq2d notbid biimp3a syl311anc orim12d wb oprex
      mpd preq12bg mpanr12 anbi1d rexbidv 2rexbidv andir df-3an orbi12i 3ancoma
      cvv orbi2i 3bitr2i rexbii 2rexbii r19.43 bitri 3bitri syl6bb mpbird ex wi
      pythagtriplem2 impbid ) AGHZBGHZCGHZIZAJKLZBJKLZUALZCJKLZMZABUBDUCZEUCZJK
      LZFUCZJKLZUDLZNLZXIJXJXLNLNLZNLZUBMZCXIXKXMUALNLMZOZDGPZEGPFGPZXCXHYBXCXH
      OZYBAXOMZBXQMZXSIZDGPZEGPZFGPZBXOMZAXQMZXSIZDGPZEGPZFGPZQZYCJAABUELZRLSUM
      UFZJBYQRLZSUMZUFZQZYPXCUUBXHWTXAUUBXBABUGTUHYCYRYIUUAYOXCXHYRYIABCDEFUIUJ
      XCXHUUAYOXCXHUUAIXAWTXBXEXDUALZXGMZJBBAUELZRLZSUMZUFZYOWTXAXBXHUUAUKWTXAX
      BXHUUAULWTXAXBXHUUAUNXCXHUUDUUAXCXHUUDXCXFUUCXGXCXDVDHZXEVDHZXFUUCMWTXAUU
      IXBWTXDGHUUIAUOXDUPUQURXAWTUUJXBXAXEGHUUJBUOXEUPUQUSXDXEUTVAVBVCTXCXHUUAU
      UHYCYTUUGYCYSUUFJSYCYQUUEBRYCAVEHZBVEHZYQUUEMXCUUKXHWTXAUUKXBAVFURUHXCUUL
      XHXAWTUULXBBVFUSUHABVGVAVHVIVJVKBACDEFUIVLUJVMVPXCYBYPVNZXHWTXAUUMXBWTXAO
      ZYBYDYEOZYKYJOZQZXSOZDGPZEGPFGPZYPUUNYAUUSFEGGUUNXTUURDGUUNXRUUQXSUUNXOWF
      HXQWFHXRUUQVNXIXNNVOXIXPNVOABXOXQGGWFWFVQVRVSVTWAUUTYFYLQZDGPZEGPFGPYGYMQ
      ZEGPZFGPZYPUUSUVBFEGGUURUVADGUURUUOXSOZUUPXSOZQYFYKYJXSIZQUVAUUOUUPXSWBYF
      UVFUVHUVGYDYEXSWCYKYJXSWCWDUVHYLYFYKYJXSWEWGWHWIWJUVBUVCFEGGYFYLDGWKWJUVE
      YHYNQZFGPYPUVDUVIFGYGYMEGWKWIYHYNFGWKWLWMWNTUHWOWPWTXAYBXHWQXBABCDEFWRTWS
      $.
      $( [19-Apr-2014] $) $( [19-Apr-2014] $)
  $}

