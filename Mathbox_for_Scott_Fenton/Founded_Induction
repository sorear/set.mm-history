$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Founded Induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a b x $.  $d B a b c y $.  $d R a b c x $.  $d R y $.
    $( Every (possibly proper) subclass of a class ` A ` with a founded,
       set-like relation ` R ` has a minimal element.  Lemma 4.3 of Don Monk's
       notes for Advanced Set Theory, which can be found at
       ~ http://euclid.colorado.edu/~~monkd/settheory .  This is a very strong
       generalization of ~ tz6.26 and ~ tz7.5 . $)
    frmin $p |- ( ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                     ( B C_ A /\ B =/= (/) ) ) ->
                   E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( va vb vc wfr cv cpred cvv wcel wral wa wss c0 wne wceq wrex wi frss
      ssralv predpredss ssexg ex syl ralimdv syld cbvsetlike syl5ib anim12d
      wex weq predeq3 eqeq1d rcla4ev adantl ctrcl setlikespec trclpred ssn0
      trclss jctild adantr trclex sseq1 neeq1 anbi12d predeq2 rexeqbi1dv
      imbi12d imbi2d wal dffr4 ax-4 sylbi vtocl sspred trcltr imp sylanc
      biimprd reximdva ssrexv sylan9r ancom31s pm2.61dne 19.23adv n0 syl6com
      imp32 ) CEIZCEAJKLMACNZOZDCPZDQRZDEBJZKZQSZBDTZWPWODEIZDEFJZKZLMZFDNZOZWQ
      XAUAWPWMXBWNXFDCEUBWPCEXCKZLMZFCNZXFWNWPXJXIFDNXFXIFDCUCWPXIXEFDWPXDXHPZX
      IXEUADCEXCUDXKXIXEXDXHLUEUFUGUHUIAFCEUJUKULXGGJZDMZGUMXAWQXGXMXAGXGXMXAXG
      XMOXADEXLKZQXMXNQSZXAUAXGXMXOXAWTXOBXLDBGUNWSXNQDEWRXLUOUPUQUFURXMXFXBXNQ
      RZXAUAXMXFOZXBOXPDEXLUSZDPZXRQRZOZXAXQXPYAUAZXBXQXNLMZYBFDEXLUTZYCXPXTXSY
      CXNXRPZXPXTUADLEXLVAYEXPXTXNXRVBUFUGDLEXLVCZVDUGVEXBYAXREWRKZQSZBXRTZXQXA
      XBHJZDPZYJQRZOZYJEWRKZQSZBYJTZUAZUAXBYAYIUAZUAHXRDEXLVFYJXRSZYQYRXBYSYMYA
      YPYIYSYKXSYLXTYJXRDVGYJXRQVHVIYOYHBYJXRYSYNYGQYJXREWRVJUPVKVLVMXBYQHVNYQH
      BDEVOYQHVPVQVRXQYIWTBXRTZXAXQYHWTBXRXQWRXRMZOZWTYHUUBWSYGQXSWSXRPZWSYGSUU
      BDXREWRVSXQXSUUAXQYCXSYDYFUGZVEXQUUAUUCFDEXLWRVTWAWBUPWCWDXQXSYTXAUAUUDWT
      BXRDWEUGUIWFUIWGWHUFWIGDWJUKWKWL $.
      $( [4-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B y $.  $d R x $.  $d R y $.
    $( The principle of founded induction.  Theorem 4.4 of Don Monk's notes
       (see ~ frmin ).  This principle states that if ` B ` is a subclass of a
       founded class ` A ` with the property that every element of ` B ` whose
       initial segment is included in ` A ` is is itself equal to ` A ` .
       Compare ~ wfi and ~ tfi , which are special cases of this theorem that
       do not require the transitive closure to prove. $)
    frind $p |- ( ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                     ( B C_ A /\
                       A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) ) ->
                   A = B ) $=
      ( wfr cv cpred cvv wcel wral wa wss wi cdif c0 wne wn difss wceq wrex
      frmin eldif anbi1i anass ancom ccnv csn cima cin indif2 df-pred incom
      eqtri difeq1i 3eqtr4i eqeq1i ssdif0 bitr4i bitri anbi2i 3bitri rexbii2
      rexanali sylib ex mpani necon3bbii syl5ib con4d imp adantrl simprl eqssd
      ) CEFCEAGHIJACKLZDCMZCEBGZHZDMZVQDJZNBCKZLLCDVOWACDMZVPVOWAWBVOWBWAVOCDOZ
      PQZWARZWBRVOWCCMZWDWECDSVOWFWDLZWEVOWGLWCEVQHZPTZBWCUAZWEABCWCEUBWJVSVTRZ
      LZBCUAWEWIWLBWCCVQWCJZWILVQCJZWKLZWILWNWKWILZLWNWLLWMWOWIVQCDUCUDWNWKWIUE
      WPWLWNWPWIWKLWLWKWIUFWIVSWKWIVRDOZPTVSWHWQPEUGVQUHUIZWCUJZWRCUJZDOWHWQWRC
      DUKWHWCWRUJWSWCEVQULWCWRUMUNVRWTDVRCWRUJWTCEVQULCWRUMUNUOUPUQVRDURUSUDUTV
      AVBVCVSVTBCVDUTVEVFVGWBWCPCDURVHVIVJVKVLVOVPWAVMVN $.
      $( [6-Feb-2011] $)

    ${
      frind.1 $e |- R Fr A $.
      frind.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
      $( The principle of founded induction.  Theorem 4.4 of Don Monk's notes
         (see ~ frmin ).  This principle states that if ` B ` is a subclass of
         a founded class ` A ` with the property that every element of ` B `
         whose initial segment is included in ` A ` is is itself equal to
         ` A ` . $)
      frindi $p |- ( ( B C_ A /\
                         A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) ->
                        A = B ) $=
        ( wfr cv cpred cvv wcel wral wa wss wi wceq pm3.2i frind mpan ) CEHZCEA
        IJKLACMZNDCOCEBIZJDOUCDLPBCMNCDQUAUBFGRABCDEST $.
        $( [6-Feb-2011] $)
    $}

  $}

  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    frinsg.1 $e |- ( y e. A ->
                       ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Founded Induction Schema.  If a property passes from all elements less
       than ` y ` of a founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` . $)
    frinsg $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( vw wfr cv cpred cvv wcel wral wa crab wceq wss wi ssrab2 wsb ax-17
      hbs1 hbral hbim weq eleq1 predeq3 raleqdv sbequ12 imbi12d chvar dfss3
      elrabsf pm3.27bi ralimi sylbi syl5 anc2li syl6ibr rgen frind mpanr12
      rabid2 sylib ) EFIEFBJKLMBENOZEACEPZQZACENVFVGEREFHJZKZVGRZVIVGMZSZHENVHA
      CETVMHEVIEMZVKVNACHUAZOVLVNVKVOVNACDUAZDVJNZVOVKCJZEMZVPDEFVRKZNZASZSVNVQ
      VOSZSCHVNWCCVNCUBZVQVOCVPCDVJDJZVJMCUBACDUCUDACHUCUEUECHUFZVSVNWBWCVRVIEU
      GWFWAVQAVOWFVPDVTVJEFVRVIUHUIACHUJUKUKGULVKWEVGMZDVJNVQDVJVGUMWGVPDVJWGWE
      EMVPACHWEEWDUNUOUPUQURUSACHVIEWDUNUTVABHEVGFVBVCACEVDVE $.
      $( [7-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A w y z $.  $d ph w z $.  $d R x $.  $d R w y z $.
    frins.1 $e |- R Fr A $.
    frins.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins.3 $e |- ( y e. A ->
                      ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Founded Induction Schema.  If a property passes from all elements less
       than ` y ` of a founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` . $)
    frins $p |- ( y e. A -> ph ) $=
      ( wfr cv cpred cvv wcel wral frinsg mp2an rspec ) ACEEFJEFBKLMNBEOACEOGHA
      BCDEFIPQR $.
      $( [6-Feb-2011] $)
  $}


  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    frins2fg.1 $e |- ( y e. A ->
                         ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    frins2fg.2 $e |- ( ps -> A. y ps ) $.
    frins2fg.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Founded Induction schema, using implicit substitution. $)
    frins2fg $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( cv wcel cpred wral wsbc sbie ralbii syl5ib frinsg ) ACDEFGDKZFLBEFGTMZN
      AADEKOZEUANHUBBEUAABDEIJPQRS $.
      $( [7-Feb-2011] $)
  $}


  ${
    $d A x $.  $d A y z $.  $d ph z $.  $d R x $.  $d R y z $.
    frins2f.1 $e |- R Fr A $.
    frins2f.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins2f.3 $e |- ( ps -> A. y ps ) $.
    frins2f.4 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins2f.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Founded Induction schema, using implicit substitution. $)
    frins2f $p |- ( y e. A -> ph ) $=
      ( wfr cv cpred cvv wcel wral frins2fg mp2an rspec ) ADFFGMFGCNOPQCFRADFRH
      IABCDEFGLJKSTUA $.
      $( [6-Feb-2011] $)
  $}

  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    $d ps y $.
    frins2g.1 $e |- ( y e. A ->
                         ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    frins2g.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Founded Induction schema, using implicit substitution. $)
    frins2g $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( ax-17 frins2fg ) ABCDEFGHBDJIK $.
      $( [8-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y z $.  $d ph z $.  $d ps y $.  $d R x $.  $d R y z $.
    frins2.1 $e |- R Fr A $.
    frins2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins2.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins2.4 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Founded Induction schema, using implicit substitution. $)
    frins2 $p |- ( y e. A -> ph ) $=
      ( ax-17 frins2f ) ABCDEFGHIBDLJKM $.
      $( [6-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y z $.  $d B y $.  $d ph z $.  $d ps y $.  $d ch y $.
    $d R x $.  $d R y z $.
    frins3.1 $e |- R Fr A $.
    frins3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins3.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins3.4 $e |- ( y = B -> ( ph <-> ch ) ) $.
    frins3.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.


    $( Founded Induction schema, using implicit substitution. $)
    frins3 $p |- ( B e. A -> ch ) $=
      ( frins2 vtoclga ) ACEHGMABDEFGIJKLNOP $.
      $( [6-Feb-2011] $)
  $}

