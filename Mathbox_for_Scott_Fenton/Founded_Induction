$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Founded Induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a b x $.  $d B a b c y $.  $d R a b c x $.  $d R y $.
    $( Every (possibly proper) subclass of a class ` A ` with a founded,
       set-like relation ` R ` has a minimal element.  Lemma 4.3 of Don Monk's
       notes for Advanced Set Theory, which can be found at
       ~ http://euclid.colorado.edu/~~monkd/settheory .  This is a very strong
       generalization of ~ tz6.26 and ~ tz7.5 .  (Contributed by Scott Fenton,
       4-Feb-2011.) $)
    frmin $p |- ( ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                     ( B C_ A /\ B =/= (/) ) ) ->
                   E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( va vb vc wfr cv cpred cvv wcel wral wa wss c0 wne wceq wrex wi frss
      cbvsetlike ssralv predpredss ssexg ex syl ralimdv syld syl5bi anim12d
      wex n0 weq predeq3 eqeq1d rcla4ev adantl ctrpred setlikespec trpredpred
      ssn0 trpredss jctild adantr trpredex sseq1 neeq1 anbi12d predeq2
      rexeqbi1dv imbi12d imbi2d wal dffr4 ax-4 sylbi vtocl trpredtr imp sspred
      syl11anc biimprd reximdva ssrexv sylsyld sylan9r an31s pm2.61dne
      19.23adv syl6com imp32 ) CEIZCEAJKLMACNZOZDCPZDQRZDEBJZKZQSZBDTZWQWPDEIZD
      EFJZKZLMZFDNZOZWRXBUAWQWNXCWOXGDCEUBWOCEXDKZLMZFCNZWQXGAFCEUCWQXKXJFDNXGX
      JFDCUDWQXJXFFDWQXEXIPZXJXFUADCEXDUEXLXJXFXEXILUFUGUHUIUJUKULWRGJZDMZGUMXH
      XBGDUNXHXNXBGXHXNXBXHXNOXBDEXMKZQXNXOQSZXBUAXHXNXPXBXAXPBXMDBGUOWTXOQDEWS
      XMUPUQURUGUSXNXGXCXOQRZXBUAXNXGOZXCOXQDEXMUTZDPZXSQRZOZXBXRXQYBUAZXCXRXOL
      MZYCFDEXMVAZYDXQYAXTYDXOXSPZXQYAUADLEXMVBYFXQYAXOXSVCUGUHDLEXMVDZVEUHVFXC
      YBXSEWSKZQSZBXSTZXRXBXCHJZDPZYKQRZOZYKEWSKZQSZBYKTZUAZUAXCYBYJUAZUAHXSDEX
      MVGYKXSSZYRYSXCYTYNYBYQYJYTYLXTYMYAYKXSDVHYKXSQVIVJYPYIBYKXSYTYOYHQYKXSEW
      SVKUQVLVMVNXCYRHVOYRHBDEVPYRHVQVRVSXRXTYJXABXSTXBXRYDXTYEYGUHZXRYIXABXSXR
      WSXSMZOZXAYIUUCWTYHQUUCXTWTXSPZWTYHSXRXTUUBUUAVFXRUUBUUDFDEXMWSVTWADXSEWS
      WBWCUQWDWEXABXSDWFWGWHUJWIWJUGWKUKWLWM $.
      $( [4-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B y $.  $d R x $.  $d R y $.
    $( The principle of founded induction.  Theorem 4.4 of Don Monk's notes
       (see ~ frmin ).  This principle states that if ` B ` is a subclass of a
       founded class ` A ` with the property that every element of ` B ` whose
       initial segment is included in ` A ` is is itself equal to ` A ` .
       Compare ~ wfi and ~ tfi , which are special cases of this theorem that
       do not require the axiom of infinity to prove.  (Contributed by Scott
       Fenton, 6-Feb-2011.) $)
    frind $p |- ( ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                     ( B C_ A /\
                       A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) ) ->
                   A = B ) $=
      ( wfr cv cpred cvv wcel wral wa wss wi wn cdif c0 wne ssdif0 necon3bbii
      difss wceq wrex frmin eldif anbi1i anass ancom ccnv csn cima cin indif2
      df-pred incom eqtri difeq1i 3eqtr4i eqeq1i bitr4i bitri anbi2i 3bitri
      rexbii2 rexanali sylib ex mpani syl5bi con4d imp adantrl simprl eqssd )
      CEFCEAGHIJACKLZDCMZCEBGZHZDMZVQDJZNBCKZLLCDVOWACDMZVPVOWAWBVOWBWAWBOCDPZQ
      RZVOWAOZWBWCQCDSTVOWCCMZWDWECDUAVOWFWDLZWEVOWGLWCEVQHZQUBZBWCUCZWEABCWCEU
      DWJVSVTOZLZBCUCWEWIWLBWCCVQWCJZWILVQCJZWKLZWILWNWKWILZLWNWLLWMWOWIVQCDUEU
      FWNWKWIUGWPWLWNWPWIWKLWLWKWIUHWIVSWKWIVRDPZQUBVSWHWQQEUIVQUJUKZWCULZWRCUL
      ZDPWHWQWRCDUMWHWCWRULWSWCEVQUNWCWRUOUPVRWTDVRCWRULWTCEVQUNCWRUOUPUQURUSVR
      DSUTUFVAVBVCVDVSVTBCVEVAVFVGVHVIVJVKVLVOVPWAVMVN $.
      $( [6-Feb-2011] $)

    ${
      frind.1 $e |- R Fr A $.
      frind.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
      $( The principle of founded induction.  Theorem 4.4 of Don Monk's notes
         (see ~ frmin ).  This principle states that if ` B ` is a subclass of
         a founded class ` A ` with the property that every element of ` B `
         whose initial segment is included in ` A ` is is itself equal to
         ` A ` .  (Contributed by Scott Fenton, 6-Feb-2011.) $)
      frindi $p |- ( ( B C_ A /\
                         A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) ->
                        A = B ) $=
        ( wfr cv cpred cvv wcel wral wa wss wi wceq pm3.2i frind mpan ) CEHZCEA
        IJKLACMZNDCOCEBIZJDOUCDLPBCMNCDQUAUBFGRABCDEST $.
        $( [6-Feb-2011] $)
    $}

  $}

  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    frinsg.1 $e |- ( y e. A ->
                       ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Founded Induction Schema.  If a property passes from all elements less
       than ` y ` of a founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` .
       (Contributed by Scott Fenton, 7-Feb-2011.) $)
    frinsg $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( vw wfr cv cpred cvv wcel wral wa crab wceq wss wi ssrab2 wsb dfss3
      ax-17 elrabsf simprbi ralimi sylbi hbs1 hbral hbim weq eleq1 predeq3
      raleqdv sbequ12 imbi12d chvar syl5 anc2li syl6ibr rgen frind mpanr12
      rabid2 sylib ) EFIEFBJKLMBENOZEACEPZQZACENVFVGEREFHJZKZVGRZVIVGMZSZHENVHA
      CETVMHEVIEMZVKVNACHUAZOVLVNVKVOVKACDUAZDVJNZVNVOVKDJZVGMZDVJNVQDVJVGUBVSV
      PDVJVSVREMVPACHVREVNCUCZUDUEUFUGCJZEMZVPDEFWAKZNZASZSVNVQVOSZSCHVNWFCVTVQ
      VOCVPCDVJVRVJMCUCACDUHUIACHUHUJUJCHUKZWBVNWEWFWAVIEULWGWDVQAVOWGVPDWCVJEF
      WAVIUMUNACHUOUPUPGUQURUSACHVIEVTUDUTVABHEVGFVBVCACEVDVE $.
      $( [7-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A w y z $.  $d ph w z $.  $d R x $.  $d R w y z $.
    frins.1 $e |- R Fr A $.
    frins.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins.3 $e |- ( y e. A ->
                      ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Founded Induction Schema.  If a property passes from all elements less
       than ` y ` of a founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` .
       (Contributed by Scott Fenton, 6-Feb-2011.) $)
    frins $p |- ( y e. A -> ph ) $=
      ( wfr cv cpred cvv wcel wral frinsg mp2an rspec ) ACEEFJEFBKLMNBEOACEOGHA
      BCDEFIPQR $.
      $( [6-Feb-2011] $)
  $}


  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    frins2fg.1 $e |- ( y e. A ->
                         ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    frins2fg.2 $e |- ( ps -> A. y ps ) $.
    frins2fg.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Founded Induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 7-Feb-2011.) $)
    frins2fg $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( cv wsbc cpred wral wcel sbie ralbii syl5bi frinsg ) ACDEFGADEKLZEFGDKZM
      ZNBEUBNUAFOATBEUBABDEIJPQHRS $.
      $( [7-Feb-2011] $)
  $}


  ${
    $d A x $.  $d A y z $.  $d ph z $.  $d R x $.  $d R y z $.
    frins2f.1 $e |- R Fr A $.
    frins2f.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins2f.3 $e |- ( ps -> A. y ps ) $.
    frins2f.4 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins2f.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Founded Induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 6-Feb-2011.) $)
    frins2f $p |- ( y e. A -> ph ) $=
      ( wfr cv cpred cvv wcel wral frins2fg mp2an rspec ) ADFFGMFGCNOPQCFRADFRH
      IABCDEFGLJKSTUA $.
      $( [6-Feb-2011] $)
  $}

  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d R w y z $.  $d R x $.
    $d ps y $.
    frins2g.1 $e |- ( y e. A ->
                         ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    frins2g.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    $( Founded Induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 8-Feb-2011.) $)
    frins2g $p |- ( ( R Fr A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( ax-17 frins2fg ) ABCDEFGHBDJIK $.
      $( [8-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y z $.  $d ph z $.  $d ps y $.  $d R x $.  $d R y z $.
    frins2.1 $e |- R Fr A $.
    frins2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins2.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins2.4 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Founded Induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 6-Feb-2011.) $)
    frins2 $p |- ( y e. A -> ph ) $=
      ( ax-17 frins2f ) ABCDEFGHIBDLJKM $.
      $( [6-Feb-2011] $)
  $}

  ${
    $d A x $.  $d A y z $.  $d B y $.  $d ph z $.  $d ps y $.  $d ch y $.
    $d R x $.  $d R y z $.
    frins3.1 $e |- R Fr A $.
    frins3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    frins3.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    frins3.4 $e |- ( y = B -> ( ph <-> ch ) ) $.
    frins3.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.


    $( Founded Induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 6-Feb-2011.) $)
    frins3 $p |- ( B e. A -> ch ) $=
      ( frins2 vtoclga ) ACEHGMABDEFGIJKLNOP $.
      $( [6-Feb-2011] $)
  $}


