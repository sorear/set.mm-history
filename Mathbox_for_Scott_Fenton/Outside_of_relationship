$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Outside of relationship
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c OutsideOf $.

  $( Declare the syntax for the outside of constant. $)
  coutsideof $a class OutsideOf $.

  $( The outside of relationship.  This relationship expresses that ` P ` ,
     ` A ` , and ` B ` fall on a line, but ` P ` is not on the segment
     ` A B ` .  This definition is taken from theorem 6.4 of [Schwabhauser]
     p. 43, since it requires no dummy variables. $)
  df-outsideof $a |- OutsideOf = ( Colinear \ Btwn ) $.

  $( Binary relationship form of ` OutsideOf ` .  Theorem 6.4 of [Schwabhauser]
     p. 43.  (Contributed by Scott Fenton, 17-Oct-2013.) $)
  broutsideof $p |- ( P OutsideOf <. A , B >. <->
    ( P Colinear <. A , B >. /\ -. P Btwn <. A , B >. ) ) $=
    ( cop coutsideof wbr ccolin cbtwn cdif wn wa df-outsideof breqi brdif bitri
    ) CABDZEFCPGHIZFCPGFCPHFJKCPEQLMCPGHNO $.
    $( [19-Apr-2014] $) $( [17-Oct-2013] $)

  $( Alternate form of ` OutsideOf ` .  Definition 6.1 of [Schwabhauser]
     p. 43.  (Contributed by Scott Fenton, 17-Oct-2013.) $)
  broutsideof2 $p |- ( ( N e. NN /\
    ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
    ( P OutsideOf <. A , B >. <->
      ( A =/= P /\ B =/= P /\
        ( A Btwn <. P , B >. \/ B Btwn <. P , A >. ) ) ) ) $=
    ( cop wbr cbtwn wn wcel w3a wne wceq 3adant3r1 breq1 syl5ibcom necon3bd imp
    wa adantrl wi coutsideof ccolin cee cfv broutsideof btwntriv1 btwntriv2 w3o
    cn wo brcolinear pm2.24 a1i wb 3anrot btwncom sylan2b orc syl6bi a1dd 3jaod
    olc sylbid imp32 3jca simp3 3ancomb btwncolinear2 btwncolinear1 jaod simpr2
    a1d syl5 df-ne sylib simprl1 simprr simpl simpr1 simpr3 btwnswapid syl13anc
    adantr mp2and expr mtod 3exp2 btwncomand sylan2br com12 com4l 3imp2 impbida
    jca syl5bb ) CABEZUAFCWPUBFZCWPGFZHZRZDUIIZCDUCUDZIZAXBIZBXBIZJZRZACKZBCKZA
    CBEGFZBCAEGFZUJZJZABCUEXGWTXMXGWTRXHXIXLXGWSXHWQXGWSXHXGWRACXGAWPGFZACLZWRX
    AXDXEXNXCABDUFMACWPGNOPQSXGWSXIWQXGWSXIXGWRBCXGBWPGFZBCLZWRXAXDXEXPXCABDUGM
    BCWPGNOPQSXGWQWSXLXGWQWRABCEGFZXKUHWSXLTZCABDUKXGWRXSXRXKWRXSTXGWRXLULUMXGX
    RXLWSXGXRXJXLXFXAXDXEXCJXRXJUNXCXDXEUOABCDUPUQXJXKURUSUTXKXSTXGXKXLWSXKXJVB
    VLUMVAVCVDVEXGXMRWQWSXGXMWQXMXLXGWQXHXIXLVFXGXJWQXKXFXAXCXEXDJXJWQTXCXDXEVG
    CBADVHUQCABDVIVJVMQXGXHXIXLWSXLXGXHXIWSXGXLXHXIWSTTZXGXJXTXKXGXJXHXIWSXGXJX
    HXIJZRZWRXOYBXHXOHXGXJXHXIVKACVNVOXGYAWRXOXGYAWRRZRXJWRXOXJXHXIWRXGVPXGYAWR
    VQXGXJWRRXOTZYCXGXAXDXCXEYDXAXFVRZXAXCXDXEVKZXAXCXDXEVSZXAXCXDXEVTZACBDWAWB
    WCWDWEWFWGXGXKXHXIWSXGXKXHXIJZRZWRXQYJXIXQHXGXKXHXIVTBCVNVOXGYIWRXQXGYIWRRZ
    RXKCBAEGFZXQXKXHXIWRXGVPXGYKCABDYEYGYFYHXGYIWRVQWHXGXKYLRXQTZYKXFXAXEXCXDJY
    MXEXCXDUOBCADWAWIWCWDWEWFWGVJWJWKWLWNWMWO $.
    $( [19-Apr-2014] $) $( [17-Oct-2013] $)

  $( Outsideness implies inequality.  (Contributed by Scott Fenton,
     18-Oct-2013.) $)
  outsidene1 $p |- ( ( N e. NN /\
   ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
   ( P OutsideOf <. A , B >. -> A =/= P ) ) $=
    ( cn wcel cee cfv w3a wa cop coutsideof wbr wne cbtwn wo broutsideof2 simp1
    syl6bi ) DEFCDGHZFATFBTFIJCABKLMACNZBCNZACBKOMBCAKOMPZIUAABCDQUAUBUCRS $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  $( Outsideness implies inequality.  (Contributed by Scott Fenton,
     18-Oct-2013.) $)
  outsidene2 $p |- ( ( N e. NN /\
   ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
   ( P OutsideOf <. A , B >. -> B =/= P ) ) $=
    ( cn wcel cee cfv w3a wa cop coutsideof wbr wne cbtwn wo broutsideof2 simp2
    syl6bi ) DEFCDGHZFATFBTFIJCABKLMACNZBCNZACBKOMBCAKOMPZIUBABCDQUAUBUCRS $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  $( A principle linking outsideness to betweenness.  Theorem 6.2 of
     [Schwabhauser] p. 43.  (Contributed by Scott Fenton, 18-Oct-2013.) $)
  btwnoutside $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ P e. ( EE ` N ) ) ) ->
    ( ( ( A =/= P /\ B =/= P /\ C =/= P ) /\ P Btwn <. A , C >. ) ->
      ( P Btwn <. B , C >. <-> P OutsideOf <. A , B >. ) ) ) $=
    ( wcel wa w3a wne cop cbtwn wbr df-3an simpr2 btwncomand simpr3 3jca adantr
    sylan2br expr cn cee cfv coutsideof wb simpr11 simpr12 simpr13 simp1 simp3r
    simp2l simp3l simp2r btwnconn2 3com23 mpd btwnouttr2 syl122anc btwnexch3and
    wo wi simp3 jaod syl5 impbid broutsideof2 syl13anc bitr4d ex ) EUAFZAEUBUCZ
    FZBVKFZGZCVKFZDVKFZGZHZADIZBDIZCDIZHZDACJKLZGZDBCJKLZDABJUDLZUEVRWDGZWEVSVT
    ADBJKLZBDAJKLZUTZHZWFWGWEWKVRWDWEWKWDWEGVRWBWCWEHZWKWBWCWEMVRWLGZVSVTWJVSVT
    WAWCWEVRUFVSVTWAWCWEVRUGWMWADCAJKLZDCBJKLZHZWJWMWAWNWOVSVTWAWCWEVRUHVRWLDAC
    EVJVNVQUIZVJVNVOVPUJZVJVLVMVQUKZVJVNVOVPULZVRWBWCWENOVRWLDBCEWQWRVJVLVMVQUM
    ZWTVRWBWCWEPOQVRWPWJVAZWLVJVQVNXBCDABEUNUORUPQSTWKWJWGWEVSVTWJVBWGWHWEWIVRW
    DWHWEWDWHGVRWBWCWHHZWEWBWCWHMVRXCGZVSABDJKLZWCHZWEXDVSXEWCVSVTWAWCWHVRUFVRX
    CADBEWQWSWRXAVRWBWCWHPOVRWBWCWHNQVRXFWEVAZXCVRVJVMVLVPVOXGWQXAWSWRWTBADCEUQ
    URRUPSTVRWDWIWEWDWIGVRWBWCWIHZWEWBWCWIMVRXHABDCEWQWSXAWRWTVRXHBDAEWQXAWRWSV
    RWBWCWIPOVRWBWCWINUSSTVCVDVEVRWFWKUEZWDVRVJVPVLVMXIWQWRWSXAABDEVFVGRVHVI $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  ${
    $d N c $.  $d A c $.  $d B c $.  $d P c $.
    $( Characterization of outsideness in terms of relationship to a fourth
       point.  Theorem 6.3 of [Schwabhauser] p. 43.  (Contributed by Scott
       Fenton, 18-Oct-2013.) $)
    broutsideof3 $p |- ( ( N e. NN /\
       ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
       ( P OutsideOf <. A , B >. <->
         ( A =/= P /\ B =/= P /\
           E. c e. ( EE ` N ) ( c =/= P /\ P Btwn <. A , c >. /\
                                P Btwn <. B , c >. ) ) ) ) $=
      ( wcel w3a wa cop wbr wne cbtwn simpr3 adantr wi df-3an btwncomand simpr2
      wrex expr cn cee cfv coutsideof wo cv broutsideof2 simpl simpr1 syl111anc
      btwndiff 3anass necomd simp1 simp23 simp22 simp21 simp3 btwnexch3and 3jca
      simpr1r syl2anbr an32s reximdva jaod simprr1 simpll simplr1 simplr2 simpr
      mpd simprr2 simplr3 simprr3 btwnconn2 syl122anc rexlimdva impbid pm5.32da
      3bitr4g bitrd ) DUAFZCDUBUCZFZAWCFZBWCFZGZHZCABIUDJACKZBCKZACBILJZBCAILJZ
      UEZGZWIWJEUFZCKZCAWOILJZCBWOILJZGZEWCSZGZABCDUGWHWIWJHZWMHXBWTHWNXAWHXBWM
      WTWHXBHZWMWTXCWKWTWLWHXBWKWTWHXBWKHZHZWRCWOKZHZEWCSZWTWHXHXDWHWBWFWDXHWBW
      GUHZWBWDWEWFMWBWDWEWFUIZBCDEUKUJNXEXGWSEWCWHWOWCFZXDXGWSOWHXKHZXDXGWSXLWB
      WGXKGZXDWRXFGZWSXDXGHWBWGXKPZXDWRXFULXMXNHZWPWQWRXPCWOXMXDWRXFMUMXMXNBACW
      ODWBWGXKUNZWBWDWEWFXKUOZWBWDWEWFXKUPZWBWDWEWFXKUQZWBWGXKURZXMXNACBDXQXSXT
      XRXBWKWRXFXMVAQXMXDWRXFRZUSYBUTVBTVCVDVKTWHXBWLWTWHXBWLHZHZWQXFHZEWCSZWTW
      HYFYCWHWBWEWDYFXIWBWDWEWFRXJACDEUKUJNYDYEWSEWCWHXKYCYEWSOXLYCYEWSXLXMYCWQ
      XFGZWSYCYEHXOYCWQXFULXMYGHZWPWQWRYHCWOXMYCWQXFMUMXMYCWQXFRZXMYGABCWODXQXS
      XRXTYAXMYGBCADXQXRXTXSXBWLWQXFXMVAQYIUSUTVBTVCVDVKTVEXCWSWMEWCWHXKXBWSWMO
      XLXBWSWMXLXBWSHZHZWPCWOAILJZCWOBILJZGZWMYKWPYLYMWPWQWRXBXLVFXLYJCAWODWBWG
      XKVGZWDWEWFWBXKVHZWDWEWFWBXKVIZWHXKVJZWPWQWRXBXLVLQXLYJCBWODYOYPWDWEWFWBX
      KVMZYRWPWQWRXBXLVNQUTXLYNWMOZYJXLWBXKWDWEWFYTYOYRYPYQYSWOCABDVOVPNVKTVCVQ
      VRVSWIWJWMPWIWJWTPVTWA $.
      $( [19-Apr-2014] $) $( [18-Oct-2013] $)
  $}

  $( Reflexitivity of outsideness.  Theorem 6.5 of [Schwabhauser] p. 44.
     (Contributed by Scott Fenton, 18-Oct-2013). $)
  outsideofrflx $p |- ( ( N e. NN /\ P e. ( EE ` N ) /\ A e. ( EE ` N ) ) ->
    ( A =/= P -> P OutsideOf <. A , A >. ) ) $=
    ( cn wcel cee cfv w3a wne cop ccolin wbr cbtwn wn coutsideof axbtwnid eqcom
    wa wceq syl6ib necon3ad colineartriv2 jctild broutsideof syl6ibr ) CDEBCFGZ
    EAUFEHZABIZBAAJZKLZBUIMLZNZRBUIOLUGUHULUJUGUKABUGUKBASABSBACPBAQTUABACUBUCA
    ABUDUE $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  $( Commutitivity law for outsideness.  Theorem 6.6 of [Schwabhauser] p. 44.
     (Contributed by Scott Fenton, 18-Oct-2013). $)
  outsideofcom $p |- ( ( N e. NN /\
    ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
    ( P OutsideOf <. A , B >. <-> P OutsideOf <. B , A >. ) ) $=
    ( cn cee cfv w3a wa wne cop cbtwn wbr wo coutsideof wb 3ancoma broutsideof2
    wcel orcom 3anbi3i bitri a1i 3ancomb sylan2b 3bitr4d ) DESZCDFGZSZAUHSZBUHS
    ZHZIZACJZBCJZACBKLMZBCAKLMZNZHZUOUNUQUPNZHZCABKOMCBAKOMZUSVAPUMUSUOUNURHVAU
    NUOURQURUTUOUNUPUQTUAUBUCABCDRULUGUIUKUJHVBVAPUIUJUKUDBACDRUEUF $.
    $( [19-Apr-2014] $) $( [18-Oct-2013] $)

  ${
    $( Transitivity law for outsideness.  Theorem 6.7 of [Schwabhauser] p. 44.
       (Contributed by Scott Fenton, 18-Oct-2013.) $)
    outsideoftr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) )
          /\
    ( C e. ( EE ` N ) /\ P e. ( EE ` N ) ) ) ->
    ( ( P OutsideOf <. A , B >. /\ P OutsideOf <. B , C >. ) ->
      P OutsideOf <. A , C >. ) ) $=
      ( wcel wa w3a wne cop cbtwn wo coutsideof wi simprr 3jca df-3an expr jaod
      wbr cn cee simpll simplr simplr1 simp1 simp3r simp2l simp2r simp3l simpr2
      cfv simplr3 simpr3 btwnexchand sylan2br btwnconn3 syl122anc adantr mp2and
      orcd simprlr simpll2 adantl necomd btwnconn1 mpd olcd imp32 exp31 syl5 wb
      imp3a broutsideof2 syl13anc anbi12d anbi12i an4 bitr4i syl6bb 3imtr4d ) E
      UAFZAEUBULZFZBWCFZGZCWCFZDWCFZGZHZADIZBDIZGZWLCDIZGZGZADBJZKTZBDAJZKTZLZB
      DCJZKTZCWQKTZLZGZGZWKWNAXBKTZCWSKTZLZHZDABJMTZDBCJMTZGZDACJMTZWJWPXFXKWPW
      KWLWNHZWJXFXKNWPWKWLWNWKWLWOUCWKWLWOUDWMWLWNOPWJXPXFXKWJXPGZXFGWKWNXJWKWL
      WNWJXFUEWKWLWNWJXFUMXQXAXEXJXQWRXEXJNZWTWJXPWRXRWJXPWRGZGXCXJXDWJXSXCXJXS
      XCGWJXPWRXCHZXJXPWRXCQWJXTGXHXIWJXTDABCEWBWFWIUFZWBWFWGWHUGZWBWDWEWIUHZWB
      WDWEWIUIZWBWFWGWHUJZWJXPWRXCUKWJXPWRXCUNUOVAUPRWJXSXDXJWJXSXDGZGWRXDXJWJX
      PWRXDVBWJXSXDOWJWRXDGXJNZYFWJWBWHWDWGWEYGYAYBYCYEYDDACBEUQURUSUTRSRWJXPWT
      XRWJXPWTGZGXCXJXDWJYHXCXJWJYHXCGZGZDBIZWTXCHZXJYJYKWTXCYJBDYIWLWJWKWLWNWT
      XCVCVDVEWJXPWTXCVBWJYHXCOPWJYLXJNZYIWJWBWHWEWDWGYMYAYBYDYCYEDBACEVFURUSVG
      RWJYHXDXJYHXDGWJXPWTXDHZXJXPWTXDQWJYNGXIXHWJYNDCBAEYAYBYEYDYCWJXPWTXDUNWJ
      XPWTXDUKUOVHUPRSRSVIPVJVKVMWJXNWKWLXAHZWLWNXEHZGZXGWJXLYOXMYPWJWBWHWDWEXL
      YOVLYAYBYCYDABDEVNVOWJWBWHWEWGXMYPVLYAYBYDYEBCDEVNVOVPYQWMXAGZWOXEGZGXGYO
      YRYPYSWKWLXAQWLWNXEQVQWMWOXAXEVRVSVTWJWBWHWDWGXOXKVLYAYBYCYEACDEVNVOWA $.
      $( [19-Apr-2014] $) $( [18-Oct-2013] $)
  $}

  $( Uniqueness law for ` OutsideOf ` .  Analog of ~ segconeq .  (Contributed
     by Scott Fenton, 24-Oct-2013.) $)
  outsideofeq $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ R e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ X e. ( EE ` N ) /\ Y e. ( EE ` N ) ) ) ->
    ( ( ( A OutsideOf <. X , R >. /\ <. A , X >. Cgr <. B , C >. ) /\
        ( A OutsideOf <. Y , R >. /\ <. A , Y >. Cgr <. B , C >. ) ) ->
      X = Y ) ) $=
    ( wcel w3a cop wbr wa wne cbtwn simprlr simprrr simprll exp32 endofsegidand
    adantl cn cee cfv coutsideof ccgr wo wceq simp21 simp32 simp22 broutsideof2
    wb syl13anc anbi1d simp33 anbi12d simpll3 simprl3 jca simpll2 simp23 simp31
    simp1 cgrtr3and jca32 midofsegid syl122anc adantr mpd btwnexchand cgrcomand
    3jca eqcomd simprrl necomd btwnconn1 simprr simplrr expr jaod ccased syldan
    wi imp32 ex sylbid ) EUAHZAEUBUCZHZDWHHZBWHHZIZCWHHZFWHHZGWHHZIZIZAFDJUDKZA
    FJZBCJZUEKZLZAGDJUDKZAGJZWTUEKZLZLFAMZDAMZFADJZNKZDWSNKZUFZIZXALZGAMZXHGXIN
    KZDXDNKZUFZIZXELZLZFGUGZWQXBXNXFXTWQWRXMXAWQWGWIWNWJWRXMULWGWLWPVCZWGWIWJWK
    WPUHZWGWLWMWNWOUIZWGWIWJWKWPUJZFDAEUKUMUNWQXCXSXEWQWGWIWOWJXCXSULYCYDWGWLWM
    WNWOUOZYFGDAEUKUMUNUPWQYAYBWQYAXLXRLZXHWSXDUEKZLZLYBWQYALYHXHYIYAYHWQYAXLXR
    XGXHXLXAXTUQXOXHXRXEXNURUSTYAXHWQXGXHXLXAXTUTTWQYAAFAGBCEYCYDYEYDYGWGWIWJWK
    WPVAWGWLWMWNWOVBWQXMXAXTOWQXNXSXEPVDVEWQYHYJYBWQXJXPXKXQYJYBWCWQXJXPLZYJYBW
    QYKYJLZLZXJXPYIIZYBYMXJXPYIWQXJXPYJQWQXJXPYJOWQYKXHYIPVLWQYNYBWCZYLWQWGWIWJ
    WNWOYOYCYDYFYEYGADFGEVFVGVHVIRWQXKXPLZYJYBWQYPYJLZAFGEYCYDYEYGWQYQAGDFEYCYD
    YGYFYEWQXKXPYJOWQXKXPYJQVJWQYPXHYIPSRWQXJXQLZYJYBWQYRYJLZLGFWQYSAGFEYCYDYGY
    EWQYSAFDGEYCYDYEYFYGWQXJXQYJQWQXJXQYJOVJWQYSAFAGEYCYDYEYDYGWQYRXHYIPVKSVMRW
    QXKXQLZYJYBWQYTYJLZLZFXDNKZGWSNKZUFZYBUUBADMZXKXQIZUUEUUBUUFXKXQUUBDAWQYTXH
    YIVNVOWQXKXQYJQWQXKXQYJOVLWQUUGUUEWCZUUAWQWGWIWJWNWOUUHYCYDYFYEYGADFGEVPVGV
    HVIUUBUUCYBUUDWQUUAUUCYBWQUUAUUCLZLGFWQUUIAGFEYCYDYGYEWQUUAUUCVQWQUUIAFAGEY
    CYDYEYDYGUUIYIWQYTXHYIUUCVRTVKSVMVSWQUUAUUDYBWQUUAUUDLZAFGEYCYDYEYGWQUUAUUD
    VQUUJYIWQYTXHYIUUDVRTSVSVTVIRWAWDWBWEWF $.
    $( [19-Apr-2014] $) $( [24-Oct-2013] $)

  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d C x $.  $d C y $.  $d N x $.
    $d N y $.  $d R x $.  $d R y $.  $d x y $.
    $( Given a non-degenerate ray, there is a unique point congruent to the
       segment ` B C ` lying on the ray ` A R ` .  Theorem 6.11 of
       [Schwabhauser] p. 44.  (Contributed by Scott Fenton, 23-Oct-2013.) $)
    outsideofeu $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ R e. ( EE ` N ) ) /\
      ( B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( ( R =/= A /\ B =/= C ) ->
        E! x e. ( EE ` N ) ( A OutsideOf <. x , R >. /\
         <. A , x >. Cgr <. B , C >. ) ) ) $=
      ( vy wcel wa w3a wne cv cop coutsideof wbr ccgr wrex adantr wb 3jca cn wi
      cee cfv wreu weq wral cbtwn wo segcon2 simpl2l simpr simpl2r broutsideof2
      simpl1 syl13anc simpllr adantl wceq simprlr simp2l anim1i simpl3 cgrdegen
      simp3 syl111anc necon3bid mpbird necomd simplll simprr expr impbid2 bitrd
      mpd orcom syl6bb pm5.32rd rexbidva simpl3l simpl3r simprl outsideofeq imp
      an32s syl2an an4s exp32 ralrimivv opeq1 breq2d opeq2 breq1d reu4 sylanbrc
      anbi12d ex ) FUAHZBFUCUDZHZEWSHZIZCWSHZDWSHZIZJZEBKZCDKZIZBALZEMZNOZBXJMZ
      CDMZPOZIZAWSUEZXFXIIZXPAWSQZXPBGLZEMZNOZBXTMZXNPOZIZIZAGUFZUBZGWSUGAWSUGX
      QXRXSEXMUHOZXJBEMUHOZUIZXOIZAWSQZXFYMXIAECDBFUJRXRXPYLAWSXFXJWSHZXIXPYLSX
      FYNIZXIIXOXLYKYOXIXOXLYKSYOXIXOIZIZXLYJYIUIZYKYQXLXJBKZXGYRJZYRYOXLYTSZYP
      YOWRWTYNXAUUAWRXBXEYNUOZWTXAWRXEYNUKXFYNULWTXAWRXEYNUMXJEBFUNUPRYQYTYRYSX
      GYRVEYOYPYRYTYOYPYRIZIZYSXGYRUUDBXJUUDBXJKXHUUCXHYOXGXHXOYRUQURUUDBXJCDUU
      DXOBXJUSCDUSSZYOXIXOYRUTYOXOUUEUBZUUCYOWRWTYNIXEUUFUUBXFWTYNWRWTXAXEVAVBW
      RXBXEYNVCBXJCDFVDVFRVOVGVHVIUUCXGYOXGXHXOYRVJURYOYPYRVKTVLVMVNYJYIVPVQVLV
      RWEVSVHXRYHAGWSWSXRYNXTWSHZIZYFYGXFUUHXIYFYGXFUUHIZWRWTXAXCJZXDYNUUGJZJZY
      FYGXIYFIUUIWRUUJUUKWRXBXEUUHUOUUIWTXAXCWTXAWRXEUUHUKWTXAWRXEUUHUMXCXDWRXB
      UUHVTTUUIXDYNUUGXCXDWRXBUUHWAXFYNUUGWBXFYNUUGVKTTXIYFULUULYFYGBCDEFXJXTWC
      WDWFWGWHWIXPYEAGWSYGXLYBXOYDYGXKYABNXJXTEWJWKYGXMYCXNPXJXTBWLWMWPWNWOWQ
      $.
      $( [19-Apr-2014] $) $( [23-Oct-2013] $)
  $}

  ${
    $d A y $.  $d B y $.  $d P y $.  $d N y $.
    $( Relate ` OutsideOf ` to ` Seg<_ ` .  Theorem 6.13 of [Schwabhauser]
       p. 45.  (Contributed by Scott Fenton, 24-Oct-2013.) $)
    outsidele $p |- ( ( N e. NN /\
    ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ) ->
    ( P OutsideOf <. A , B >. ->
      ( <. P , A >. Seg<_ <. P , B >. <-> A Btwn <. P , B >. ) ) ) $=
      ( vy wcel w3a wa cop coutsideof cbtwn wb ccgr simpr3 adantr wceq ad2antrr
      wbr wi mpd cn cee cfv csegle cv wrex simpl simpr1 simpr2 syl122anc simprl
      brsegle2 outsideofcom mpbid simpll simplr1 simplr3 cgrrflxd jca ccolin wn
      simprrl simpr simplr2 btwncolinear1 syl13anc wne outsidene1 df-ne simpr2l
      sylib df-3an btwncomand btwnswapid2 mp2and sylan2br expr mtod broutsideof
      sylanbrc simprrr outsideofeq syl133anc opeq2 breq2d syl5ibrcom an4s exp32
      rexlimdv sylbid btwnsegle impbid ex ) DUAFZCDUBUCZFZAWOFZBWOFZGZHZCABIJRZ
      CAICBIZUDRZAXBKRZLWTXAHZXCXDXEXCACEUEZIZKRZXGXBMRZHZEWOUFZXDWTXCXKLZXAWTW
      NWPWQWPWRXLWNWSUGWNWPWQWRUHZWNWPWQWRUIXMWNWPWQWRNECACBDULUJOXEXJXDEWOXEXF
      WOFZXJXDWTXNXAXJXDWTXNHZXAXJHZHZBXFPZXDXQCBAIJRZXBXBMRZHZCXFAIZJRZXIHZXRX
      QXSXTXQXAXSXOXAXJUKZWTXAXSLXNXPABCDUMQUNXOXTXPXOCBDWNWSXNUOZWPWQWRWNXNUPZ
      WPWQWRWNXNUQZUROUSXQYCXIXQCYBUTRZCYBKRZVAYCXQXHYIXOXAXHXIVBZXOXHYISZXPXOW
      NWPXNWQYLYFYGWTXNVCZWPWQWRWNXNVDZCXFADVEVFOTXQYJACPZXQACVGZYOVAXQXAYPYEWT
      XAYPSXNXPABCDVHQTACVIVKXOXPYJYOXPYJHXOXAXJYJGZYOXAXJYJVLXOYQHAXFCIKRZYJYO
      XOYQACXFDYFYNYGYMXHXIXAYJXOVJVMXOXAXJYJNXOYRYJHYOSZYQXOWNWQXNWPYSYFYNYMYG
      AXFCDVNVFOVOVPVQVRXFACVSVTXOXAXHXIWAUSXOYAYDHXRSZXPXOWNWPWQWPWRWRXNYTYFYG
      YNYGYHYHYMCCBADBXFWBWCOVOXQXDXRXHYKXRXBXGAKBXFCWDWEWFTWGWHWIWJWTXDXCSXACA
      BDWKOWLWM $.
      $( [19-Apr-2014] $) $( [24-Oct-2013] $)
  $}

  $( Outside of implies colinearity.  (Contributed by Scott Fenton,
     26-Oct-2013.) $)
  outsideofcol $p |- ( P OutsideOf <. Q , R >. -> P Colinear <. Q , R >. ) $=
    ( cop coutsideof wbr ccolin cbtwn wn broutsideof simplbi ) ABCDZEFALGFALHFI
    BCAJK $.
    $( [19-Apr-2014] $) $( [26-Oct-2013] $)

