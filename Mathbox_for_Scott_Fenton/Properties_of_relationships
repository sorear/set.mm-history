$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of relationships
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    $( Membership in a restriction $)
    elres $p |- ( A e. ( B |` C ) <-> E. x e. C E. y ( A = <. x , y >. /\
                                                        <. x , y >. e. B ) ) $=
      ( cres wcel cv cop wceq wa wex wrex wrel relres elrel mpan eleq1 biimpd
      visset opelres biimpi ancomd syl6com ancld an12 syl6ib 19.22dvv mpd
      rexcom4 df-rex exbii excom 3bitri sylibr biimpri expcom biimprd syl9
      imp3a 19.23adv r19.23aiv impbii ) CDEFZGZCAHZBHZIZJZVHDGZKZBLZAEMZVEVFEGZ
      VKKZBLALZVMVEVIBLALZVPVDNVEVQDEOABCVDPQVEVIVOABVEVIVIVNVJKZKVOVEVIVRVIVEV
      HVDGZVRVIVEVSCVHVDRZSVSVJVNVSVJVNKZVFVGDEBTUAZUBUCUDUEVIVNVJUFUGUHUIVMVKA
      EMZBLVOALZBLVPVKABEUJWCWDBVKAEUKULVOBAUMUNUOVLVEAEVNVKVEBVNVIVJVEVNVJVSVI
      VEVJVNVSVSWAWBUPUQVIVEVSVTURUSUTVAVBVC $.
      $( [27-Mar-2011] $) $( [17-Mar-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    elsnres.1 $e |- C e. _V $.
    $( Memebership in restriction to a singleton $)
    elsnres $p |- ( A e. ( B |` { C } ) <-> E. y ( A = <. C , y >. /\
                                                    <. C , y >. e. B ) ) $=
      ( vx csn cres wcel cv cop wceq wa wex wrex elres rexcom4 df-rex elsn
      anbi1i exbii opeq1 eqeq2d eleq1d anbi12d biimpa 19.23aiv eqeq1 weq
      visset opth equid mpbiran2 syl6bb anbi1d anbi2d exbidv eqid cla4ev mpan
      anass eqcom anbi2i anidm bitri bitr3i sylibr syl5bir imp impbii 3bitri )
      BCDGZHIBFJZAJZKZLZVOCIZMZANFVLOVRFVLOZANBDVNKZLZVTCIZMZANFABCVLPVRFAVLQVS
      WCAVSVMVLIZVRMZFNVMDLZVRMZFNZWCVRFVLRWEWGFWDWFVRFDSTUAWHWCWGWCFWFVRWCWFVP
      WAVQWBWFVOVTBVMDVNUBZUCWFVOVTCWIUDZUEUFUGWAWBWHWAWHWFDVMLZVQMZMZFNZWBWAWG
      WMFWAVRWLWFWAVPWKVQWAVPVTVOLZWKBVTVOUHWOWKAAUIDVNVMVNEAUJZWPUKAULUMUNUOUP
      UQWBWFVQMZFNZWNDDLZWBWRDURWQWSWBMFDEWFWFWSVQWBVMDDUHWJUEUSUTWMWQFWMWFWKMZ
      VQMWQWFWKVQVAWTWFVQWTWFWFMWFWKWFWFDVMVBVCWFVDVETVFUAVGVHVIVJVKUAVK $.
      $( [27-Mar-2011] $) $( [17-Mar-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The epsilon relation and membership are the same.  General version of
       ~ epel . $)
    epelg $p |- ( ( A e. C /\ B e. D ) -> ( A _E B <-> A e. B ) ) $=
      ( vx vy cv cep wbr wel wb wcel wceq breq1 eleq1 bibi12d breq2 eleq2 epel
      vtocl2g ) EGZFGZHIZEFJZKAUBHIZAUBLZKABHIZABLZKEFABCDUAAMUCUEUDUFUAAUBHNUA
      AUBOPUBBMUEUGUFUHUBBAHQUBBARPEFST $.
      $( [27-Mar-2011] $) $( [27-Mar-2011] $)
  $}

  $( The difference of two binary relations $)
  brdif $p |- ( A ( R \ S ) B <-> ( A R B /\ -. A S B ) ) $=
    ( cop cdif wcel wn wa wbr eldif df-br notbii anbi12i 3bitr4i ) ABEZCDFZGPCG
    ZPDGZHZIABQJABCJZABDJZHZIPCDKABQLUARUCTABCLUBSABDLMNO $.
    $( [22-Apr-2011] $) $( [11-Apr-2011] $)

  ${
    $d A z $.  $d X z $.
    ressn0.1 $e |- X e. _V $.
    ressn0.2 $e |- Y e. _V $.
    $( If ` X ` is not in ` A ` , then the restriction of a singleton of
       ` <. X , Y >. ` to ` A ` is null $)
    ressn0 $p |- ( -. X e. A -> ( { <. X , Y >. } |` A ) = (/) ) $=
      ( vz wcel wn csn cin cvv cxp c0 cop cres wceq wo cv wal wa elin elsni
      eleq1d biimpa sylbi con3i 19.21aiv eq0 sylibr orcd xpeq0 df-res xpsn
      ineq1i inxp 3eqtr2i syl5eq ) BAGZHZBIZAJZCIZKJZLZMBCNIZAOZUSVAMPZVCMPZQVD
      MPUSVGVHUSFRZVAGZHZFSVGUSVKFVJURVJVIUTGZVIAGZTURVIUTAUAVLVMURVLVIBAVIBUBU
      CUDUEUFUGFVAUHUIUJVAVCUKUIVFVEAKLZJUTVBLZVNJVDVEAULVOVEVNBCDEUMUNUTVBAKUO
      UPUQ $.
      $( [22-Apr-2011] $) $( [15-Apr-2011] $)
  $}

  ${
    brtp.1 $e |- X e. _V $.
    brtp.2 $e |- Y e. _V $.
    brtp.3 $e |- B e. _V $.
    brtp.4 $e |- D e. _V $.
    brtp.5 $e |- F e. _V $.
    $( A condition for a binary relation over an unordered triple $)
    brtp $p |- ( X { <. A , B >. , <. C , D >. , <. E , F >. } Y <->
                 ( ( X = A /\ Y = B ) \/
                   ( X = C /\ Y = D ) \/
                   ( X = E /\ Y = F ) ) ) $=
      ( cop ctp wbr wcel wceq w3o wa df-br opex eltp opth 3orbi123i 3bitri ) GH
      ABNZCDNZEFNZOZPGHNZUJQUKUGRZUKUHRZUKUIRZSGARHBRTZGCRHDRTZGERHFRTZSGHUJUAU
      KUGUHUIGHUBUCULUOUMUPUNUQGHABIJKUDGHCDIJLUDGHEFIJMUDUEUF $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( A quantifier free definition of a founded relationship $)
    dffr5 $p |- ( R Fr A <->
                  ~P A C_ ( { (/) } u. ran ( _E \ ( _E o. `' R ) ) ) ) $=
      ( vx vz vy cv cpw wcel c0 csn cep ccnv ccom cdif crn cun wi wal wss wne
      wa wbr wn wral wrex wfr wo elun imbi2i df-or impexp visset elpw elsn
      necon3bbii anbi12i wex wel brdif epel brcnv ancom bitri exbii brco
      df-rex 3bitr4i notbii ralnex bitr4i elrn imbi12i bitr3i 3bitri albii
      dfss2 df-fr 3bitr4ri ) CFZAGZHZVSIJZKKBLZMZNZOZPZHZQZCRVSASZVSITZUAZDFZEF
      ZBUBZUCDVSUDZEVSUEZQZCRVTWGSABUFWIWRCWIWAVSWBHZVSWFHZUGZQWAWSUCZWTQZQZWRW
      HXAWAVSWBWFUHUIXAXCWAWSWTUJUIXDWAXBUAZWTQWRWAXBWTUKXEWLWTWQWAWJXBWKVSACUL
      ZUMWSVSICIUNUOUPWNVSWEUBZEUQECURZWPUAZEUQWTWQXGXIEXGWNVSKUBZWNVSWDUBZUCZU
      AXIWNVSKWDUSXJXHXLWPECUTXLWODVSUEZUCWPXKXMWNWMWCUBZWMVSKUBZUAZDUQDCURZWOU
      AZDUQXKXMXPXRDXPWOXQUAXRXNWOXOXQWNWMBEULZDULVADCUTUPWOXQVBVCVDDWNVSKWCXSX
      FVEWODVSVFVGVHWODVSVIVJUPVCVDEVSWEXFVKWPEVSVFVGVLVMVNVOCVTWGVPCEDABVQVR
      $.
      $( [22-Apr-2011] $) $( [11-Apr-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30. $)
    frirrc $p |- ( ( R Fr A /\ X e. A ) -> -. X R X ) $=
      ( vx wfr wcel wbr wn cv wa wi wceq eleq1 anbi2d breq1 breq2 bitrd notbid
      imbi12d frirr vtoclg anabsi7 ) ABEZCAFZCCBGZHZUCDIZAFZJZUGUGBGZHZKUCUDJZU
      FKDCAUGCLZUIULUKUFUMUHUDUCUGCAMNUMUJUEUMUJCUGBGUEUGCUGBOUGCCBPQRSDABTUAUB
      $.
      $( [22-Apr-2011] $) $( [22-Apr-2011] $)
  $}

