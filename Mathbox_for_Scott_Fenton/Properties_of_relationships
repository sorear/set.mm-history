$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of relationships
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    $( Membership in a restriction. $)
    elres $p |- ( A e. ( B |` C ) <-> E. x e. C E. y ( A = <. x , y >. /\
                                                        <. x , y >. e. B ) ) $=
      ( cres wcel cv cop wceq wa wex wrex wrel relres elrel mpan eleq1 biimpd
      visset opelres biimpi ancomd syl6com ancld an12 syl6ib 2eximdv mpd
      rexcom4 df-rex exbii excom 3bitri sylibr biimpri expcom biimprd syl9
      imp3a 19.23adv r19.23aiv impbii ) CDEFZGZCAHZBHZIZJZVHDGZKZBLZAEMZVEVFEGZ
      VKKZBLALZVMVEVIBLALZVPVDNVEVQDEOABCVDPQVEVIVOABVEVIVIVNVJKZKVOVEVIVRVIVEV
      HVDGZVRVIVEVSCVHVDRZSVSVJVNVSVJVNKZVFVGDEBTUAZUBUCUDUEVIVNVJUFUGUHUIVMVKA
      EMZBLVOALZBLVPVKABEUJWCWDBVKAEUKULVOBAUMUNUOVLVEAEVNVKVEBVNVIVJVEVNVJVSVI
      VEVJVNVSVSWAWBUPUQVIVEVSVTURUSUTVAVBVC $.
      $( [17-Mar-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d C x y $.
    elsnres.1 $e |- C e. _V $.
    $( Memebership in restriction to a singleton. $)
    elsnres $p |- ( A e. ( B |` { C } ) <-> E. y ( A = <. C , y >. /\
                                                    <. C , y >. e. B ) ) $=
      ( vx csn cres wcel cv cop wceq wa wex wrex elres rexcom4 opeq1 eqeq2d
      eleq1d anbi12d rexsn exbii 3bitri ) BCDGZHIBFJZAJZKZLZUHCIZMZANFUEOUKFUEO
      ZANBDUGKZLZUMCIZMZANFABCUEPUKFAUEQULUPAUKUPFDEUFDLZUIUNUJUOUQUHUMBUFDUGRZ
      SUQUHUMCURTUAUBUCUD $.
      $( [17-Mar-2011] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    epelcNEW.1 $e |- B e. _V $.
    $( The epsilon relationship and the membership relation are the same. $)
    epelcNEW $p |- ( A _E B <-> A e. B ) $=
      ( vx vy cep wbr cvv wcel rele brrelexi elisset wb wel cv eleq1 eleq2
      df-eprel brabg mpan2 pm5.21nii ) ABFGZAHIZABIZABFJKABLUCBHIUBUDMCDENAEOZI
      UDDEABHHFDOAUEPUEBAQDERSTUA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The epsilon relation and membership are the same.  General version of
       ~ epel . $)
    epelg $p |- ( B e. C -> ( A _E B <-> A e. B ) ) $=
      ( vx cv cep wbr wcel wb wceq breq2 eleq2 bibi12d visset epelcNEW vtoclg
      ) ADEZFGZAQHZIABFGZABHZIDBCQBJRTSUAQBAFKQBALMAQDNOP $.
      $( [27-Mar-2011] $)
  $}

  ${
    brtp.1 $e |- X e. _V $.
    brtp.2 $e |- Y e. _V $.
    brtp.3 $e |- B e. _V $.
    brtp.4 $e |- D e. _V $.
    brtp.5 $e |- F e. _V $.
    $( A condition for a binary relation over an unordered triple. $)
    brtp $p |- ( X { <. A , B >. , <. C , D >. , <. E , F >. } Y <->
                 ( ( X = A /\ Y = B ) \/
                   ( X = C /\ Y = D ) \/
                   ( X = E /\ Y = F ) ) ) $=
      ( cop ctp wbr wcel wceq w3o wa df-br opex eltp opth 3orbi123i 3bitri ) GH
      ABNZCDNZEFNZOZPGHNZUJQUKUGRZUKUHRZUKUIRZSGARHBRTZGCRHDRTZGERHFRTZSGHUJUAU
      KUGUHUIGHUBUCULUOUMUPUNUQGHABIJKUDGHCDIJLUDGHEFIJMUDUEUF $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( A quantifier free definition of a founded relationship. $)
    dffr5 $p |- ( R Fr A <->
                  ~P A C_ ( { (/) } u. ran ( _E \ ( _E o. `' R ) ) ) ) $=
      ( vx vz vy cv cpw wcel c0 csn cep ccnv ccom cdif crn cun wi wal wss wne
      wa wbr wn wral wrex wfr visset elpw wo elun df-or elsn necon3bbii wex
      wel brdif epel brco ancom brcnv anbi12i bitri exbii df-rex dfrex2
      3bitr2ri con1bii elrn 3bitr4i imbi12i 3bitri impexp bitr4i albii dfss2
      df-fr 3bitr4ri ) CFZAGZHZVRIJZKKBLZMZNZOZPZHZQZCRVRASZVRITZUADFZEFZBUBZUC
      DVRUDZEVRUEZQZCRVSWFSABUFWHWPCWHWIWJWOQZQWPVTWIWGWQVRACUGZUHWGVRWAHZVRWEH
      ZUIWSUCZWTQWQVRWAWEUJWSWTUKXAWJWTWOWSVRICIULUMWLVRWDUBZEUNECUOZWNUAZEUNWT
      WOXBXDEXBWLVRKUBZWLVRWCUBZUCZUAXDWLVRKWCUPXEXCXGWNECUQWNXFXFDCUOZWMUAZDUN
      ZWMDVRUEWNUCXFWLWKWBUBZWKVRKUBZUAZDUNXJDWLVRKWBEUGZWRURXMXIDXMXLXKUAXIXKX
      LUSXLXHXKWMDCUQWLWKBXNDUGUTVAVBVCVBWMDVRVDWMDVRVEVFVGVAVBVCEVRWDWRVHWNEVR
      VDVIVJVKVJWIWJWOVLVMVNCVSWFVOCEDABVPVQ $.
      $( [11-Apr-2011] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( Another quantifier free definition of a founded relationship.  (The
       proof was shortened by Andrew Salmon, 27-Aug-2011.) $)
    dffr6 $p |- ( R Fr A <->
                   ( ~P A \ { (/) } ) C_ ran ( _E \ ( _E o. `' R ) ) ) $=
      ( wfr cpw c0 csn cep ccnv ccom cdif crn cun wss dffr5 ssundif bitri ) ABC
      ADZEFZGGBHIJKZLMQRJSMABNQRSOP $.
      $( [27-Aug-2011] $) $( [28-Jun-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30. $)
    frirrc $p |- ( ( R Fr A /\ X e. A ) -> -. X R X ) $=
      ( vx wfr wcel wbr wn cv wa wi wceq eleq1 anbi2d breq1 breq2 bitrd notbid
      imbi12d frirr vtoclg anabsi7 ) ABEZCAFZCCBGZHZUCDIZAFZJZUGUGBGZHZKUCUDJZU
      FKDCAUGCLZUIULUKUFUMUHUDUCUGCAMNUMUJUEUMUJCUGBGUEUGCUGBOUGCCBPQRSDABTUAUB
      $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d A x y $.
    dftr6.1 $e |- A e. _V $.
    $( A potential definition of transitivity for sets. $)
    dftr6 $p |- ( Tr A <-> A e. ( _V \ ran ( ( _E o. _E ) \ _E ) ) ) $=
      ( vx vy wel cv wcel wa wi wal cep ccom cdif crn wn wtr cvv wbr wex elrn
      brdif visset brco epel epelcNEW anbi12i exbii bitri notbii 19.41v
      exanali bitr3i 3bitri exnal con2bii dftr2 eldif mpbiran 3bitr4i ) CDEZDFZ
      AGZHZCFZAGZIDJZCJZAKKLZKMZNZGZOZAPAQVJMGZVKVGVKVDAVIRZCSVFOZCSVGOCAVIBTVN
      VOCVNVDAVHRZVDAKRZOZHVCDSZVEOZHZVOVDAVHKUAVPVSVRVTVPVDVAKRZVAAKRZHZDSVSDV
      DAKKCUBBUCWDVCDWBUTWCVBCDUDVAABUEUFUGUHVQVEVDABUEUIUFWAVCVTHDSVOVCVTDUJVC
      VEDUKULUMUGVFCUNUMUOCDAUPVMAQGVLAQVJUQBURUS $.
      $( [22-Mar-2012] $) $( [18-Mar-2012] $)
  $}

  ${
    $d A x $.  $d B x $.  $d R x $.
    coep.1 $e |- A e. _V $.
    coep.2 $e |- B e. _V $.
    $( Composition with epsilon. $)
    coep $p |- ( A ( _E o. R ) B <-> E. x e. B A R x ) $=
      ( cv wbr cep wa wex wcel ccom wrex epelcNEW anbi2i ancom bitri exbii
      brco df-rex 3bitr4i ) BAGZDHZUCCIHZJZAKUCCLZUDJZAKBCIDMHUDACNUFUHAUFUDUGJ
      UHUEUGUDUCCFOPUDUGQRSABCIDEFTUDACUAUB $.
      $( [18-Feb-2013] $)

    $( Composition with the converse of epsilon. $)
    coepr $p |- ( A ( R o. `' _E ) B <-> E. x e. A x R B ) $=
      ( cv cep ccnv wbr wa wex wcel ccom wrex visset brcnv epelcNEW bitri
      anbi1i exbii brco df-rex 3bitr4i ) BAGZHIZJZUECDJZKZALUEBMZUHKZALBCDUFNJU
      HABOUIUKAUGUJUHUGUEBHJUJBUEHEAPQUEBERSTUAABCDUFEFUBUHABUCUD $.
      $( [18-Feb-2013] $)
  $}

