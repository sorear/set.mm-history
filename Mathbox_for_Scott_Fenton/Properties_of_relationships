$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of relationships
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x y $.  $d B x y $.
    epelcNEW.1 $e |- B e. _V $.
    $( The epsilon relationship and the membership relation are the same.
       (Contributed by Scott Fenton, 11-Apr-2012.) $)
    epelcNEW $p |- ( A _E B <-> A e. B ) $=
      ( vx vy cep wbr cvv wcel rele brrelexi elisset wb wel cv eleq1 eleq2
      df-eprel brabg mpan2 pm5.21nii ) ABFGZAHIZABIZABFJKABLUCBHIUBUDMCDENAEOZI
      UDDEABHHFDOAUEPUEBAQDERSTUA $.
      $( [11-Apr-2012] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( The epsilon relation and membership are the same.  General version of
       ~ epel .  (Contributed by Scott Fenton, 27-Mar-2011.) $)
    epelg $p |- ( B e. V -> ( A _E B <-> A e. B ) ) $=
      ( vx cv cep wbr wcel wb wceq breq2 eleq2 bibi12d visset epelcNEW vtoclg
      ) ADEZFGZAQHZIABFGZABHZIDBCQBJRTSUAQBAFKQBALMAQDNOP $.
      $( [27-Mar-2011] $)
  $}

  ${
    brtp.1 $e |- X e. _V $.
    brtp.2 $e |- Y e. _V $.
    brtp.3 $e |- B e. _V $.
    brtp.4 $e |- D e. _V $.
    brtp.5 $e |- F e. _V $.
    $( A condition for a binary relation over an unordered triple.
       (Contributed by Scott Fenton, 8-Jun-2011.) $)
    brtp $p |- ( X { <. A , B >. , <. C , D >. , <. E , F >. } Y <->
                 ( ( X = A /\ Y = B ) \/
                   ( X = C /\ Y = D ) \/
                   ( X = E /\ Y = F ) ) ) $=
      ( cop ctp wbr wcel wceq w3o wa df-br opex eltp opth 3orbi123i 3bitri ) GH
      ABNZCDNZEFNZOZPGHNZUJQUKUGRZUKUHRZUKUIRZSGARHBRTZGCRHDRTZGERHFRTZSGHUJUAU
      KUGUHUIGHUBUCULUOUMUPUNUQGHABIJKUDGHCDIJLUDGHEFIJMUDUEUF $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d R x $.  $d A x $.  $d X x $.
    $( A founded relation is irreflexive.  Special case of Proposition 6.23 of
       [TakeutiZaring] p. 30.  (Contributed by Scott Fenton, 22-Apr-2011.) $)
    frirrc $p |- ( ( R Fr A /\ X e. A ) -> -. X R X ) $=
      ( vx wfr wcel wbr wn cv wa wi wceq eleq1 anbi2d breq1 breq2 bitrd notbid
      imbi12d frirr vtoclg anabsi7 ) ABEZCAFZCCBGZHZUCDIZAFZJZUGUGBGZHZKUCUDJZU
      FKDCAUGCLZUIULUKUFUMUHUDUCUGCAMNUMUJUEUMUJCUGBGUEUGCUGBOUGCCBPQRSDABTUAUB
      $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d A x y $.
    dftr6.1 $e |- A e. _V $.
    $( A potential definition of transitivity for sets.  (Contributed by Scott
       Fenton, 18-Mar-2012.) $)
    dftr6 $p |- ( Tr A <-> A e. ( _V \ ran ( ( _E o. _E ) \ _E ) ) ) $=
      ( vx vy wel cv wcel wa wi wal cep ccom cdif crn wn wtr cvv wbr wex elrn
      brdif visset brco epel epelcNEW anbi12i exbii bitri notbii 19.41v
      exanali bitr3i 3bitri exnal con2bii dftr2 eldif mpbiran 3bitr4i ) CDEZDFZ
      AGZHZCFZAGZIDJZCJZAKKLZKMZNZGZOZAPAQVJMGZVKVGVKVDAVIRZCSVFOZCSVGOCAVIBTVN
      VOCVNVDAVHRZVDAKRZOZHVCDSZVEOZHZVOVDAVHKUAVPVSVRVTVPVDVAKRZVAAKRZHZDSVSDV
      DAKKCUBBUCWDVCDWBUTWCVBCDUDVAABUEUFUGUHVQVEVDABUEUIUFWAVCVTHDSVOVCVTDUJVC
      VEDUKULUMUGVFCUNUMUOCDAUPVMAQGVLAQVJUQBURUS $.
      $( [22-Mar-2012] $) $( [18-Mar-2012] $)
  $}

  ${
    $d A x $.  $d B x $.  $d R x $.
    coep.1 $e |- A e. _V $.
    coep.2 $e |- B e. _V $.
    $( Composition with epsilon.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    coep $p |- ( A ( _E o. R ) B <-> E. x e. B A R x ) $=
      ( cv wbr cep wa wex wcel ccom wrex epelcNEW anbi2i ancom bitri exbii
      brco df-rex 3bitr4i ) BAGZDHZUCCIHZJZAKUCCLZUDJZAKBCIDMHUDACNUFUHAUFUDUGJ
      UHUEUGUDUCCFOPUDUGQRSABCIDEFTUDACUAUB $.
      $( [18-Feb-2013] $)

    $( Composition with the converse of epsilon.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    coepr $p |- ( A ( R o. `' _E ) B <-> E. x e. A x R B ) $=
      ( cv cep ccnv wbr wa wex wcel ccom wrex visset brcnv epelcNEW bitri
      anbi1i exbii brco df-rex 3bitr4i ) BAGZHIZJZUECDJZKZALUEBMZUHKZALBCDUFNJU
      HABOUIUKAUGUJUHUGUEBHJUJBUEHEAPQUEBERSTUAABCDUFEFUBUHABUCUD $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d R x y z $.  $d A x y z $.
    $( A quantifier free definition of a founded relationship.  (Contributed by
       Scott Fenton, 11-Apr-2011.) $)
    dffr5 $p |- ( R Fr A <->
                  ( ~P A \ { (/) } ) C_ ran ( _E \ ( _E o. `' R ) ) ) $=
      ( vx vz vy cv cpw c0 csn cdif wcel cep ccnv ccom crn wi wal wss wne wa
      wbr wn wral wrex wfr eldif visset elpw elsn necon3bbii anbi12i bitri wex
      wel brdif epel coep brcnv rexbii dfrex2 3bitrri con1bii exbii elrn
      df-rex 3bitr4i imbi12i albii dfss2 df-fr 3bitr4ri ) CFZAGZHIZJZKZVLLLBMZN
      ZJZOZKZPZCQVLARZVLHSZTZDFZEFZBUAZUBDVLUCZEVLUDZPZCQVOVTRABUEWBWKCVPWEWAWJ
      VPVLVMKZVLVNKZUBZTWEVLVMVNUFWLWCWNWDVLACUGZUHWMVLHCHUIUJUKULWGVLVSUAZEUME
      CUNZWITZEUMWAWJWPWREWPWGVLLUAZWGVLVRUAZUBZTWRWGVLLVRUOWSWQXAWIECUPWIWTWTW
      GWFVQUAZDVLUDWHDVLUDWIUBDWGVLVQEUGZWOUQXBWHDVLWGWFBXCDUGURUSWHDVLUTVAVBUK
      ULVCEVLVSWOVDWIEVLVEVFVGVHCVOVTVICEDABVJVK $.
      $( [11-Apr-2011] $)
  $}

  ${
    $d A x y $.  $d R x y $.
    $( Quantifier free definition of a strict order.  (Contributed by Scott
       Fenton, 22-Feb-2013.) $)
    dfso2 $p |- ( R Or A <->
              ( R Po A /\ ( A X. A ) C_ ( R u. ( _I u. `' R ) ) ) ) $=
      ( vx vy wor wpo cv wbr wceq w3o wral wa cxp cid ccnv cun wss df-so cop
      wcel wi wal wrel wb relxp ssrel ax-mp visset opelxp wo brun ideq brcnv
      orbi12i bitr2i orbi2i 3orass 3bitr4i df-br imbi12i 2albii r2al bitr4i
      bitri anbi2i bicomi ) ABEABFZCGZDGZBHZVHVIIZVIVHBHZJZDAKCAKZLZVGAAMZBNBOZ
      PZPZQZLZCDABRWAVOVTVNVGVTVHVISZVPTZWBVSTZUAZDUBCUBZVNVPUCVTWFUDAAUECDVPVS
      UFUGWFVHATVIATLZVMUAZDUBCUBVNWEWHCDWCWGWDVMVHVIAADUHZUIVMVHVIVSHZWDVJVKVL
      UJZUJVJVHVIVRHZUJVMWJWKWLVJWLVHVINHZVHVIVQHZUJWKVHVINVQUKWMVKWNVLVHVIWIUL
      VHVIBCUHWIUMUNUOUPVJVKVLUQVHVIBVRUKURVHVIVSUSUOUTVAVMCDAAVBVCVDVEVFVD $.
      $( [22-Feb-2013] $)
  $}

  ${
    $d R x y z w $.  $d A x y z w $.
    $( Quantifier free definition of a partial ordering.  (Contributed by Scott
       Fenton, 22-Feb-2013.) $)
    dfpo2 $p |- ( R Po A <-> ( ( R i^i ( _I |` A ) ) = (/) /\
           ( ( R i^i ( A X. A ) ) o. ( R i^i ( A X. A ) ) ) C_ R ) ) $=
      ( vx vy vz vw wpo cid cres cin c0 wceq cxp ccom wss wa wb po0 res0
      ineq2i in0 eqtri xp0 coeq2i co02 0ss eqsstri pm3.2i 2th poeq2 reseq2
      ineq2d eqeq1d xpeq2 coeq2d sseq1d anbi12d bibi12d mpbiri wne cv wbr wn
      wi wral r19.28zv ralbidv bitrd r19.26 syl6bb df-po wcel disj wal cop
      opex eleq1 df-br syl6bbr visset brres cvv ididg ax-mp mpbiran bitr3i
      notbid imbi12d cla4v con2d 19.21aiv wex wrel relres elrel mpan ancri weq
      breq12 anidms a4v breq2 imbi2d biimpcd imp3a syl ideq anbi1i 3bitr3ri
      syl5ibrcom 19.23advv syl5 impbii df-ral 3bitr4i bitri wrex r2al impexp
      2albii bitr4i ralcom r19.23v ralbii relco ssrel brin anbi12i an4 ancom
      brxp anandi 3bitri anass exbii brco df-rex r19.42v imbi12i 3bitr4ri
      3bitr4g pm2.61ine ) ABGZBHAIZJZKLZBAAMZJZUUHNZBOZPZQZAKAKLZUULKBGZBHKIZJZ
      KLZUUHBAKMZJZNZBOZPZQUUNUVBBRUUQUVAUUPBKJZKUUOKBHSTBUAZUBUUTKBUUTUUHKNKUU
      SKUUHUUSUVCKUURKBAUCTUVDUBUDUUHUEUBBUFUGUHUIUUMUUCUUNUUKUVBAKBUJUUMUUFUUQ
      UUJUVAUUMUUEUUPKUUMUUDUUOBAKHUKULUMUUMUUIUUTBUUMUUHUUSUUHUUMUUGUURBAKAUNU
      LUOUPUQURUSAKUTZCVAZUVFBVBZVCZUVFDVAZBVBZUVIEVAZBVBZPZUVFUVKBVBZVDZPEAVEZ
      DAVEZCAVEZUVHCAVEZUVOEAVEZDAVEZCAVEZPZUUCUUKUVEUVRUVHUWAPZCAVEUWCUVEUVQUW
      DCAUVEUVQUVHUVTPZDAVEUWDUVEUVPUWEDAUVHUVOEAVFVGUVHUVTDAVFVHVGUVHUWACAVIVJ
      CDEABVKUUFUVSUUJUWBUUFFVAZUUDVLZVCZFBVEZUVSFBUUDVMUWFBVLZUWHVDZFVNZUVFAVL
      ZUVHVDZCVNZUWIUVSUWLUWOUWLUWNCUWLUVGUWMUWKUVGUWMVCZVDFUVFUVFVOZUVFUVFVPUW
      FUWQLZUWJUVGUWHUWPUWRUWJUWQBVLUVGUWFUWQBVQUVFUVFBVRVSUWRUWGUWMUWRUWGUWQUU
      DVLZUWMUWFUWQUUDVQUWMUVFUVFUUDVBZUWSUWTUVFUVFHVBZUWMUVFUVFHACVTZWAUVFWBVL
      UXAUXBUVFWBWCWDWEUVFUVFUUDVRWFVSWGWHWIWJWKUWOUWKFUWOUWGUWJUWGUWFUVIUVKVOZ
      LZEWLDWLZUWGPUWOUWJVCZUWGUXEUUDWMUWGUXEHAWNDEUWFUUDWOWPWQUWOUXEUWGUXFUWOU
      XDUWGUXFVDZDEUWOUXGUXDDEWRZUVIAVLZPZUVLVCZVDZUWOUXIUVIUVIBVBZVCZVDZUXLUWN
      UXOCDCDWRZUWMUXIUVHUXNUVFUVIAVQUXPUVGUXMUXPUVGUXMQUVFUVIUVFUVIBWSWTWGWHXA
      UXOUXHUXIUXKUXHUXOUXIUXKVDUXHUXNUXKUXIUXHUXMUVLUVIUVKUVIBXBWGXCXDXEXFUXDU
      WGUXJUXFUXKUXDUWGUXCUUDVLZUXJUWFUXCUUDVQUVIUVKUUDVBUVIUVKHVBZUXIPUXQUXJUV
      IUVKHAEVTZWAUVIUVKUUDVRUXRUXHUXIUVIUVKUXSXGXHXIVSUXDUWJUVLUXDUWJUXCBVLUVL
      UWFUXCBVQUVIUVKBVRVSWGWHXJXKXEXLWJWKXMUWHFBXNUVHCAXNXOXPUVMDAXQZUVNVDZEAV
      EZCAVEZUWMUVKAVLZPZUXTPZUVNVDZEVNCVNZUWBUUJUYCUYEUYAVDZEVNCVNUYHUYACEAAXR
      UYGUYICEUYEUXTUVNXSXTYAUWAUYBCAUWAUVODAVEZEAVEUYBUVODEAAYBUYJUYAEAUVMUVND
      AYCYDXPYDUUJUVFUVKVOZUUIVLZUYKBVLZVDZEVNCVNZUYHUUIWMUUJUYOQUUHUUHYECEUUIB
      YFWDUYGUYNCEUYFUYLUVNUYMUVFUVIUUHVBZUVIUVKUUHVBZPZDWLZUXIUYEUVMPZPZDWLZUY
      LUYFUYRVUADUYRUVJUVFUVIUUGVBZPZUVLUVIUVKUUGVBZPZPZUXIUYEPZUVMPZVUAUYPVUDU
      YQVUFUVFUVIBUUGYGUVIUVKBUUGYGYHVUGUVMVUCVUEPZPVUJUVMPVUIUVJVUCUVLVUEYIUVM
      VUJYJVUJVUHUVMUWMUXIPZUXIUYDPZPUXIUWMPZVULPVUJVUHVUKVUMVULUWMUXIYJXHVUCVU
      KVUEVULUVFUVIAADVTYKUVIUVKAAUXSYKYHUXIUWMUYDYLXOXHYMUXIUYEUVMYNYMYOUYSUVF
      UVKUUIVBUYLDUVFUVKUUHUUHUXBUXSYPUVFUVKUUIVRWFVUBUYTDAXQUYFUYTDAYQUYEUVMDA
      YRWFXIUVFUVKBVRYSXTYAYTYHUUAUUB $.
      $( [22-Feb-2013] $)
  $}

