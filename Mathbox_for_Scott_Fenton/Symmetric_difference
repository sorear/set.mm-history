$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Symmetric difference
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c (++) $.

  $( Declare the syntax for symmetric difference. $)
  csymdif $a class ( A (++) B ) $.

  $( Define the symmetric difference of two classes. $)
  df-symdif $a |- ( A (++) B ) = ( ( A \ B ) u. ( B \ A ) ) $.

  $( Symmetric difference commutes.  (Contributed by Scott Fenton,
     24-Apr-2012.) $)
  symdifcom $p |- ( A (++) B ) = ( B (++) A ) $=
    ( cdif cun csymdif uncom df-symdif 3eqtr4i ) ABCZBACZDJIDABEBAEIJFABGBAGH
    $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Equality theorem for symmetric difference.  (Contributed by Scott Fenton,
     24-Apr-2012.) $)
  symdifeq1 $p |- ( A = B -> ( A (++) C ) = ( B (++) C ) ) $=
    ( wceq cdif cun csymdif difeq1 difeq2 uneq12d df-symdif 3eqtr4g ) ABDZACEZC
    AEZFBCEZCBEZFACGBCGMNPOQABCHABCIJACKBCKL $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Equality theorem for symmetric difference.  (Contributed by Scott Fenton,
     24-Apr-2012.) $)
  symdifeq2 $p |- ( A = B -> ( C (++) A ) = ( C (++) B ) ) $=
    ( wceq csymdif symdifeq1 symdifcom 3eqtr4g ) ABDACEBCECAECBEABCFCAGCBGH $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  ${
    hbsymdif.1 $e |- ( x e. A -> A. y x e. A ) $.
    hbsymdif.2 $e |- ( x e. B -> A. y x e. B ) $.
    $( Hypothesis builder for symmetric difference.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    hbsymdif $p |- ( x e. ( A (++) B ) -> A. y x e. ( A (++) B ) ) $=
      ( csymdif cdif cun df-symdif hbdif hbun hbxfreq ) BACDGCDHZDCHZICDJBANOBA
      CDEFKBADCFEKLM $.
      $( [19-Feb-2013] $)
  $}

  $( Membership in a symmetric difference.  (Contributed by Scott Fenton,
     31-Mar-2012.) $)
  elsymdif $p |- ( A e. ( B (++) C ) <-> -. ( A e. B <-> A e. C ) ) $=
    ( cdif cun wcel wn wa wo csymdif wb elun eldif orbi12i bitri df-symdif
    eleq2i xor 3bitr4i ) ABCDZCBDZEZFZABFZACFZGHZUEUDGHZIZABCJZFUDUEKGUCATFZAUA
    FZIUHATUALUJUFUKUGABCMACBMNOUIUBABCPQUDUERS $.
    $( [11-Apr-2012] $) $( [31-Mar-2012] $)

  $( Symmetric difference with the empty class.  (Contributed by Scott Fenton,
     24-Apr-2012.) $)
  symdif0 $p |- ( A (++) (/) ) = A $=
    ( c0 csymdif cdif cun df-symdif dif0 0dif uneq12i un0 3eqtri ) ABCABDZBADZE
    ABEAABFLAMBAGAHIAJK $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Symmetric difference with the universal class.  (Contributed by Scott
     Fenton, 24-Apr-2012.) $)
  symdifV $p |- ( A (++) _V ) = ( _V \ A ) $=
    ( cvv csymdif cdif cun c0 df-symdif wss wceq ssv ssdif0 mpbi uneq1i uncom
    un0 eqtri 3eqtri ) ABCABDZBADZEFSEZSABGRFSABHRFIAJABKLMTSFESFSNSOPQ $.
    $( [12-May-2012] $) $( [24-Apr-2012] $)

  $( Symmetric difference yields the empty class with the same argument twice.
     (Contributed by Scott Fenton, 25-Apr-2012.) $)
  symdifid $p |- ( A (++) A ) = (/) $=
    ( csymdif cdif cun c0 df-symdif difid uneq12i un0 3eqtri ) AABAACZKDEEDEAAF
    KEKEAGZLHEIJ $.
    $( [12-May-2012] $) $( [25-Apr-2012] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Symmetric difference associates.  (Contributed by Scott Fenton,
       24-Apr-2012.) $)
    symdifass $p |- ( A (++) ( B (++) C ) ) = ( ( A (++) B ) (++) C ) $=
      ( vx csymdif cv wcel wb wn biass notbii xor3 notbi bitr4i con1bii
      3bitr3ri elsymdif bibi2i bibi1i 3bitr4i eqriv ) DABCEZEZABEZCEZDFZAGZUFUB
      GZHZIUFUDGZUFCGZHZIUFUCGUFUEGUIULUGUFBGZUKHZIZHZUGUMHZIZUKHZUIULUQUKHZIUG
      UNHZIUSUPUTVAUGUMUKJKUSUTUSIURUKIHUTURUKLUQUKMNOUGUNLPUHUOUGUFBCQRUJURUKU
      FABQSTKUFAUBQUFUDCQTUA $.
      $( [12-May-2012] $) $( [24-Apr-2012] $)
  $}

  $( The binary relationship of a symmetric difference.  (Contributed by Scott
     Fenton, 11-Apr-2012.) $)
  brsymdif $p |- ( A ( R (++) S ) B <-> -. ( A R B <-> A S B ) ) $=
    ( cop csymdif wcel wb wn wbr elsymdif df-br bibi12i notbii 3bitr4i ) ABEZCD
    FZGPCGZPDGZHZIABQJABCJZABDJZHZIPCDKABQLUCTUARUBSABCLABDLMNO $.
    $( [11-Apr-2012] $)

