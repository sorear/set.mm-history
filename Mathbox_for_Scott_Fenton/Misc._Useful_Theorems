$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Misc. Useful Theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two classes are inequal iff their intersection is a proper subset of one
     of them.  (Contributed by Scott Fenton, 23-Feb-2011.) $)
  nepss $p |- ( A =/= B <-> ( ( A i^i B ) C. A \/ ( A i^i B ) C. B ) ) $=
    ( wne cin wss wa wo wpss wn wceq nne neeq1 biimprcd orrd jctl inidm necon3i
    syl5bi adantl df-pss inss1 inss2 orim12i ineq2 syl5reqr ineq1 syl6eq impbii
    syl jaoi orbi12i bitr4i ) ABCZABDZAEZUNACZFZUNBEZUNBCZFZGZUNAHZUNBHZGUMVAUM
    UPUSGVAUMUPUSUPIUNAJZUMUSUNAKVDUSUMUNABLMRNUPUQUSUTUPUOABUAOUSURABUBOUCUIUQ
    UMUTUPUMUOABUNAABJZAAADUNAPABAUDUEQSUSUMURABUNBVEUNBBDBABBUFBPUGQSUJUHVBUQV
    CUTUNATUNBTUKUL $.
    $( [23-Feb-2011] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Closed form of ~ sneqr .  (Contributed by Scott Fenton, 1-Apr-2011.) $)
    sneqrg $p |- ( A e. V -> ( { A } = { B } -> A = B ) ) $=
      ( vx cv csn wceq wi sneq eqeq1d eqeq1 imbi12d visset sneqr vtoclg ) DEZFZ
      BFZGZPBGZHAFZRGZABGZHDACPAGZSUBTUCUDQUARPAIJPABKLPBDMNO $.
      $( [1-Apr-2011] $)

  $}

  $( Two singletons of sets are equal iff their elements are equal.
     (Contributed by Scott Fenton, 16-Apr-2012.) $)
  sneqbg $p |- ( A e. V -> ( { A } = { B } <-> A = B ) ) $=
    ( wcel csn wceq sneqrg sneq impbid1 ) ACDAEBEFABFABCGABHI $.
    $( [16-Apr-2012] $)

  ${
    3ccased.1 $e |- ( ph -> ( ( ch /\ et ) -> ps ) ) $.
    3ccased.2 $e |- ( ph -> ( ( ch /\ ze ) -> ps ) ) $.
    3ccased.3 $e |- ( ph -> ( ( ch /\ si ) -> ps ) ) $.
    3ccased.4 $e |- ( ph -> ( ( th /\ et ) -> ps ) ) $.
    3ccased.5 $e |- ( ph -> ( ( th /\ ze ) -> ps ) ) $.
    3ccased.6 $e |- ( ph -> ( ( th /\ si ) -> ps ) ) $.
    3ccased.7 $e |- ( ph -> ( ( ta /\ et ) -> ps ) ) $.
    3ccased.8 $e |- ( ph -> ( ( ta /\ ze ) -> ps ) ) $.
    3ccased.9 $e |- ( ph -> ( ( ta /\ si ) -> ps ) ) $.
    $( Triple disjunction form of ~ ccased .  (Contributed by Scott Fenton,
       27-Oct-2013.) $)
    3ccased $p |- ( ph -> ( ( ( ch \/ th \/ ta ) /\ ( et \/ ze \/ si ) ) ->
                     ps ) ) $=
      ( wa com12 3jaodan w3o wi 3jaoian ) CDEUAFGHUAZRABCUDABUBZDECFUEGHACFRBIS
      ACGRBJSACHRBKSTDFUEGHADFRBLSADGRBMSADHRBNSTEFUEGHAEFRBOSAEGRBPSAEHRBQSTUC
      S $.
      $( [19-Apr-2014] $) $( [27-Oct-2013] $)
  $}

  ${
    $d A x y z w $.  $d B x y z w $.  $d C x y z w $.  $d D x y z w $.
    $d V x y z w $.  $d W x y z w $.  $d X x y z w $.  $d Y x y z w $.
    $( Closed form of ~ preq12b .  (Contributed by Scott Fenton,
       28-Mar-2014.) $)
    preq12bg $p |- ( ( ( A e. V /\ B e. W ) /\ ( C e. X /\ D e. Y ) ) ->
       ( { A , B } = { C , D } <->
         ( ( A = C /\ B = D ) \/ ( A = D /\ B = C ) ) ) ) $=
      ( vx vy vz vw wcel wa cpr wceq wo wb wi cv weq preq1 eqeq1d eqeq1 orbi12d
      anbi1d bibi12d imbi2d preq2 anbi2d eqeq2d visset preq12b vtoclbg vtocl3ga
      eqeq2 w3a a1i 3expa impr ) AEMZBFMZNCGMZDHMZABOZCDOZPZACPZBDPZNZADPZBCPZN
      ZQZRZVAVBVCVDVOSZVDITZJTZOZKTZDOZPZIKUAZVRDPZNZVQDPZJKUAZNZQZRZSZVDAVROZW
      APZAVTPZWDNZVKWGNZQZRZSVDVEWAPZWNVINZVKBVTPZNZQZRZSVPIJKABCEFGVQAPZWJWRVD
      XEWBWMWIWQXEVSWLWAVQAVRUBUCXEWEWOWHWPXEWCWNWDVQAVTUDUFXEWFVKWGVQADUDUFUEU
      GUHVRBPZWRXDVDXFWMWSWQXCXFWLVEWAVRBAUIUCXFWOWTWPXBXFWDVIWNVRBDUDUJXFWGXAV
      KVRBVTUDUJUEUGUHVTCPZXDVOVDXGWSVGXCVNXGWAVFVEVTCDUBUKXGWTVJXBVMXGWNVHVIVT
      CAUPUFXGXAVLVKVTCBUPUJUEUGUHWKVQEMVRFMVTGMUQVSVTLTZOZPWCJLUAZNZILUAZWGNZQ
      WBWILDHXHDPZXIWAVSXHDVTUIUKXNXKWEXMWHXNXJWDWCXHDVRUPUJXNXLWFWGXHDVQUPUFUE
      VQVRVTXHIULJULKULLULUMUNURUOUSUT $.
      $( [19-Apr-2014] $) $( [28-Mar-2014] $)
  $}


