$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Misc. Useful Theorems
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Two classes are inequal iff their intersection is a proper subset of one
     of them. $)
  nepss $p |- ( A =/= B <-> ( ( A i^i B ) C. A \/ ( A i^i B ) C. B ) ) $=
    ( wne cin wss wa wo wpss wn wceq nne neeq1 biimprcd syl5bi orrd inss1 jctl
    inss2 orim12i syl inidm ineq2 syl5reqr necon3i adantl ineq1 syl6eq jaoi
    impbii df-pss orbi12i bitr4i ) ABCZABDZAEZUNACZFZUNBEZUNBCZFZGZUNAHZUNBHZGU
    MVAUMUPUSGVAUMUPUSUPIUNAJZUMUSUNAKVDUSUMUNABLMNOUPUQUSUTUPUOABPQUSURABRQSTU
    QUMUTUPUMUOABUNAABJZAAADUNAUAABAUBUCUDUEUSUMURABUNBVEUNBBDBABBUFBUAUGUDUEUH
    UIVBUQVCUTUNAUJUNBUJUKUL $.
    $( [23-Feb-2011] $)

  $( If a class is an element of a pair, then it is one of the two paired
     elements. $)
  elpri $p |- ( A e. { B , C } -> ( A = B \/ A = C ) ) $=
    ( cpr wcel wceq wo elprg ibi ) ABCDZEABFACFGABCJHI $.
    $( [1-Apr-2011] $)

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Closed form of ~ sneqr . $)
    sneqrg $p |- ( A e. V -> ( { A } = { B } -> A = B ) ) $=
      ( vx cv csn wceq wi sneq eqeq1d eqeq1 imbi12d visset sneqr vtoclg ) DEZFZ
      BFZGZPBGZHAFZRGZABGZHDACPAGZSUBTUCUDQUARPAIJPABKLPBDMNO $.
      $( [1-Apr-2011] $)

  $}

  $( Two singletons of sets are equal iff their elements are equal. $)
  sneqbg $p |- ( A e. V -> ( { A } = { B } <-> A = B ) ) $=
    ( wcel csn wceq sneqrg sneq impbid1 ) ACDAEBEFABFABCGABHI $.
    $( [12-May-2012] $) $( [16-Apr-2012] $)

  ${
    $d A x $.  $d B x $.  $d C x $.
    $( Distribute intersection over difference. $)
    indifdi $p |- ( ( A \ B ) i^i C ) = ( ( A i^i C ) \ ( B i^i C ) ) $=
      ( vx cdif cin cv wcel wn wa wo pm3.24 intnan anass mtbir biorfi an32
      andi 3bitr4i ianor anbi2i bitr4i elin eldif anbi1i bitri notbii anbi12i
      eqriv ) DABEZCFZACFZBCFZEZDGZAHZUOBHZIZJZUOCHZJZUPUTJZUQUTJZIZJZUOUKHZUOU
      NHZVAVBURUTIZKZJZVEVBURJZVKVBVHJZKVAVJVLVKVLUPUTVHJZJVMUPUTLMUPUTVHNOPUPU
      RUTQVBURVHRSVDVIVBUQUTTUAUBVFUOUJHZUTJVAUOUJCUCVNUSUTUOABUDUEUFVGUOULHZUO
      UMHZIZJVEUOULUMUDVOVBVQVDUOACUCVPVCUOBCUCUGUHUFSUI $.
      $( [14-Apr-2011] $)
  $}

