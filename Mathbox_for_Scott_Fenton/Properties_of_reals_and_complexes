$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of reals and complexes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    elicc2i.1 $e |- A e. RR $.
    elicc2i.2 $e |- B e. RR $.
    $( Inference for membership in a closed interval.  (Contributed by Scott
       Fenton, 3-Jun-2013.) $)
    elicc2i $p |- ( C e. ( A [,] B ) <-> ( C e. RR /\ A <_ C /\ C <_ B ) ) $=
      ( cr wcel cicc co cle wbr w3a wb elicc2 mp2an ) AFGBFGCABHIGCFGACJKCBJKLM
      DEABCNO $.
      $( [3-Jun-2013] $)
  $}

  ${
    sqdivzi.1 $e |- A e. CC $.
    sqdivzi.2 $e |- B e. CC $.
    $( Distribution of square over division.  (Contributed by Scott Fenton,
       7-Jun-2013.) $)
    sqdivzi $p |- ( B =/= 0 ->
            ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) ) $=
      ( cc0 wne cdiv co c2 cexp c1 cif opreq2 opreq1d opreq1 opreq2d eqeq12d cc
      wceq ax1cn keepel elimne0 sqdivi dedth ) BEFZABGHZIJHZAIJHZBIJHZGHZSAUEBK
      LZGHZIJHZUHUKIJHZGHZSBKBUKSZUGUMUJUOUPUFULIJBUKAGMNUPUIUNUHGBUKIJOPQAUKCU
      EBKRDTUABUBUCUD $.
      $( [7-Jun-2013] $)
  $}

  $( Cancellation law for division and multiplication.  (Contributed by Scott
     Fenton, 7-Jun-2013.) $)
  dmdcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) /\
         C e. CC ) ->
         ( ( A / B ) x. ( C / A ) ) = ( C / B ) ) $=
    ( cc wcel cc0 wne wa cdiv co cmul c1 wceq mulcom syl11anc opreq1d syl112anc
    divass divcl syl111anc w3a simp1l simp3 simp2l simp2r 3eqtr3rd simp1r div23
    eqtrd 3eqtr3d divid 3ad2ant1 mulid2 syl 3eqtrd ) ADEZAFGZHZBDEZBFGZHZCDEZUA
    ZABIJZCAIJZKJZAAIJZCBIJZKJZLVHKJZVHVCCVDKJZAIJZAVHKJZAIJZVFVIVCVKVMAIVCACKJ
    ZBIJZCAKJZBIJZVMVKVCVOVQBIVCUPVBVOVQMUPUQVAVBUBZURVAVBUCZACNOPVCUPVBUSUTVPV
    MMVSVTURUSUTVBUDZURUSUTVBUEZACBRQVCVBUPUSUTVRVKMVTVSWAWBCABRQUFPVCVLVEVDKJZ
    VFVCVBVDDEZUPUQVLWCMVTVCUPUSUTWDVSWAWBABSTZVSUPUQVAVBUGZCVDAUHQVCVEDEZWDWCV
    FMVCVBUPUQWGVTVSWFCASTWEVEVDNOUIVCUPVHDEZUPUQVNVIMVSVCVBUSUTWHVTWAWBCBSTZVS
    WFAVHAUHQUJURVAVIVJMVBURVGLVHKAUKPULVCWHVJVHMWIVHUMUNUO $.
    $( [7-Jun-2013] $)

  $( Swap the order of subtraction in a square.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  sqsubswap $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A - B ) ^ 2 ) = ( ( B - A ) ^ 2 ) ) $=
    ( cc wcel wa cmin co cneg c2 cexp wceq subcl sqneg negsubdi2 opreq1d eqtr3d
    syl ) ACDBCDEZABFGZHZIJGZSIJGZBAFGZIJGRSCDUAUBKABLSMQRTUCIJABNOP $.
    $( [10-Jun-2013] $)

  $( Expand the square of a subtraction.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  binom2sub $p |- ( ( A e. CC /\ B e. CC ) ->
    ( ( A - B ) ^ 2 ) =
    ( ( ( A ^ 2 ) - ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) ) $=
    ( cc wcel wa cneg caddc co cexp cmul cmin wceq negcl binom2 mulneg2 opreq2d
    c2 negsub 2cn sylancr sylan2 opreq1d axmulcl eqtr2d sqcl adantr mulcl sqneg
    syl11anc eqtr3d adantl opreq12d 3eqtr3d ) ACDZBCDZEZABFZGHZQIHZAQIHZQAUQJHZ
    JHZGHZUQQIHZGHZABKHZQIHUTQABJHZJHZKHZBQIHZGHUOUNUQCDUSVELBMAUQNUAUPURVFQIAB
    RUBUPVCVIVDVJGUPUTVHFZGHZVCVIUPVKVBUTGUPVBQVGFZJHZVKUPVAVMQJABOPUPQCDZVGCDZ
    VNVKLSABUCZQVGOTUDPUPUTCDZVHCDZVLVILUNVRUOAUEUFUPVOVPVSSVQQVGUGTUTVHRUIUJUO
    VDVJLUNBUHUKULUM $.
    $( [10-Jun-2013] $)

  ${
    binom2subi.1 $e |- A e. CC $.
    binom2subi.2 $e |- B e. CC $.
    $( Expand the square of a subtraction.  (Contributed by Scott Fenton,
       13-Jun-2013.) $)
    binom2subi $p |- ( ( A - B ) ^ 2 ) =
    ( ( ( A ^ 2 ) - ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( cc wcel cmin co c2 cexp cmul caddc wceq binom2sub mp2an ) AEFBEFABGHIJH
      AIJHIABKHKHGHBIJHLHMCDABNO $.
      $( [13-Jun-2013] $)
  $}

  $( Zero is an element of the closed unit.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  0elunit $p |- 0 e. ( 0 [,] 1 ) $=
    ( cc0 c1 cicc co wcel cr cle wbr 0re 1re elicc2i leidi lt01 ltleii mpbir3an
    ) AABCDEAFEAAGHABGHABAIJKIAILABIJMNO $.
    $( [11-Jun-2013] $)

  $( One is an element of the closed unit.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  1elunit $p |- 1 e. ( 0 [,] 1 ) $=
    ( c1 cc0 cicc co wcel cr cle wbr 0re 1re elicc2i lt01 ltleii leidi mpbir3an
    ) ABACDEAFEBAGHAAGHBAAIJKJBAIJLMAJNO $.
    $( [11-Jun-2013] $)

  $( A condition for a ratio to be a member of the closed unit.  (Contributed
     by Scott Fenton, 11-Jun-2013.) $)
  divelunit $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 < B ) ) ->
      ( ( A / B ) e. ( 0 [,] 1 ) <-> A <_ B ) ) $=
    ( cdiv co cc0 c1 cicc wcel cr cle wbr wa clt w3a 0re 1re elicc2i bitri cmul
    df-3an wb ledivmul mp3an2 adantlr wne simpll simprl gt0ne0 adantl syl111anc
    redivcl divge0 jca biantrurd cc wceq recn ad2antrl mulid1 syl breq2d syl5bb
    3bitr3d ) ABCDZEFGDHZVDIHZEVDJKZLZVDFJKZLZAIHZEAJKZLZBIHZEBMKZLZLZABJKZVEVF
    VGVINVJEFVDOPQVFVGVITRVQVIABFSDZJKZVJVRVKVPVIVTUAZVLVKFIHVPWAPAFBUBUCUDVQVH
    VIVQVFVGVQVKVNBEUEZVFVKVLVPUFVMVNVOUGVPWBVMBUHUIABUKUJABULUMUNVQVSBAJVQBUOH
    ZVSBUPVNWCVMVOBUQURBUSUTVAVCVB $.
    $( [11-Jun-2013] $)

  $( The square root function is one to one.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  sqr11 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
    ( ( sqr ` A ) = ( sqr ` B ) <-> A = B ) ) $=
    ( cr wcel cc0 cle wbr csqr cfv wb wa wi cif breq2 fveq2 bibi12d imbi12d 0re
    wceq elimel anbi1d eqeq1d eqeq1 anbi2d eqeq2d eqeq2 sqr11i dedth2h imp an4s
    ) ACDZBCDZEAFGZEBFGZAHIZBHIZSZABSZJZUKULKUMUNKZUSUKULUTUSLEUKAEMZFGZUNKZVAH
    IZUPSZVABSZJZLVBEULBEMZFGZKZVDVHHIZSZVAVHSZJZLABEEAVASZUTVCUSVGVOUMVBUNAVAE
    FNUAVOUQVEURVFVOUOVDUPAVAHOUBAVABUCPQBVHSZVCVJVGVNVPUNVIVBBVHEFNUDVPVEVLVFV
    MVPUPVKVDBVHHOUEBVHVAUFPQVAVHAECRTBECRTUGUHUIUJ $.
    $( [11-Jun-2013] $)

  ${
    pm2.61iine.1 $e |- ( ( A =/= C /\ B =/= D ) -> ph ) $.
    pm2.61iine.2 $e |- ( A = C -> ph ) $.
    pm2.61iine.3 $e |- ( B = D -> ph ) $.
    $( Equality version of ~ pm2.61ii .  (Contributed by Scott Fenton,
       13-Jun-2013.) $)
    pm2.61iine $p |- ph $=
      ( wceq wn wne df-ne syl2anbr ex pm2.61ii ) BDIZCEIZAPJZQJZARBDKCEKASBDLCE
      LFMNGHO $.
      $( [13-Jun-2013] $)
  $}

  ${
    $d A x y z w $.  $d B x y z w $.
    $( The Dedekind cut theorem.  This theorem, which may be used to replace
       ~ ax-pre-sup with appropriate adjustments, states that, if ` A `
       completely preceeds ` B ` , then there is some number separating the two
       of them.  (Contributed by Scott Fenton, 13-Jun-2013.) $)
    dedekind $p |- ( ( A C_ RR /\ B C_ RR /\ A. x e. A A. y e. B x < y ) ->
       E. z e. RR A. x e. A A. y e. B ( x <_ z /\ z <_ y ) ) $=
      ( vw cr cv clt wbr wral cle wa wrex wi c0 wn wcel ax-17 c1 wss wne simp2l
      w3a simp1l wex simp1r sylib simp2r sseld ralcom weq breq2 ralbidv rcla4cv
      n0 sylbi 3ad2ant3 jcad eximdv mpd df-rex axsup syl111anc hbra1 hb3an hban
      sylibr hbral simprrl r19.21bi wb simpl2l ssel2 sylan simplrl lenlt mpbird
      syl11anc simpl3 simp2 simpr ra4 com12 adantl adantlr adantr simplr ltnsym
      ex syld an32s ralimdva syl2an notbid cbvralv ralnex simplrr breq1 rexbidv
      imbi12d rcla4v sylc simprll anim12d exp3a ralrimd ralrimi reximdva 3expib
      mtod expr wceq 1re ral0 raleq mpbiri anbi12d 2ralbidv rcla4ev sylancr a1d
      ralrimiv pm2.61iine 3impa ) DGUAZEGUAZAHZBHZIJZBEKZADKZYHCHZLJZYMYILJZMZB
      EKZADKZCGNZYFYGMZYLMZYSODEPPDPUBZEPUBZMZYTYLYSUUDYTYLUDZYMYHIJQZADKZYHYMI
      JZYHFHZIJZFDNZOZAGKZMZCGNZYSUUEYFUUBUUHADKZCGNZUUOUUDYFYGYLUCUUBUUCYTYLUE
      UUEYMGRZUUPMZCUFZUUQUUEYMERZCUFZUUTUUEUUCUVBUUBUUCYTYLUGCEUPUHUUEUVAUUSCU
      UEUVAUURUUPUUEEGYMUUDYFYGYLUIZUJYLUUDUVAUUPOZYTYLYJADKZBEKUVDYJABDEUKUVEU
      UPBYMEBCULYJUUHADYIYMYHIUMUNUOUQURUSUTVAUUPCGVBVHCAFDVCVDUUEUUNYRCGUUEUUR
      UUNYRUUEUURUUNMZMZYQADUUEUVFAUUDYTYLAUUDASYTASYKADVEVFUURUUNAUURASUUGUUMA
      UUFADVEUULAGVEVGVGVGUVGYHDRZYPBEUUEUVFBUUDYTYLBUUDBSYTBSYKBADUVHBSZYJBEVE
      VIVFUVFBSVGUVIUVGUVHYIERZYPUVGUVHYNUVJYOUVGUVHYNUVGUVHMZYNUUFUVGUUFADUUEU
      URUUGUUMVJVKUVKYHGRZUURYNUUFVLUVGYFUVHUVLYFYGUUDYLUVFVMDGYHVNZVOUUEUURUUN
      UVHVPYHYMVQVSVRWJUUEUVFUVJYOUUEUVFUVJMZMZYOYIYMIJZQZUVOUVPYIUUIIJZFDNZUVO
      UVRQZFDKZUVSQUVOYIYHIJZQZADKZUWAUVOYLUWDUUDYTYLUVNVTUUEYTUVJYLUWDOUVNUUDY
      TYLWAUVFUVJWBZYTUVJMYKUWCADYTUVHUVJYKUWCOYTUVHMZUVJMZYKYJUWCUVJYKYJOUWFYK
      UVJYJYJBEWCWDWEUWGUVLYIGRZYJUWCOUWFUVLUVJYFUVHUVLYGUVMWFWGUWFYGUVJUWHYFYG
      UVHWHEGYIVNZVOYHYIWIVSWKWLWMWNVAUWCUVTAFDAFULUWBUVRYHUUIYIIUMWOWPUHUVRFDW
      QUHUVOUWHUUMUVPUVSOZUUEYGUVJUWHUVNUVCUWEUWIWNZUVNUUMUUEUURUUGUUMUVJWRWEUU
      LUWJAYIGABULZUUHUVPUUKUVSYHYIYMIWSUWLUUJUVRFDYHYIUUIIWSWTXAXBXCXKUVOUURUW
      HYOUVQVLUUEUURUUNUVJXDUWKYMYIVQVSVRXLXEXFXGXHXLXIVAXJDPXMZYSUUAUWMTGRZYHT
      LJZTYILJZMZBEKZADKZYSXNUWMUWSUWRAPKUWRAXOUWRADPXPXQYRUWSCTGYMTXMZYPUWQABD
      EUWTYNUWOYOUWPYMTYHLUMYMTYILWSXRXSXTZYAYBEPXMZYSUUAUXBUWNUWSYSXNUXBUWRADU
      XBUWRUVHUXBUWRUWQBPKUWQBXOUWQBEPXPXQYBYCUXAYAYBYDYE $.
      $( [13-Jun-2013] $)

    $( The Dedekind cut theorem, with the hypothesis weakened to only require
       non-strict less than.  (Contributed by Scott Fenton, 2-Jul-2013.) $)
    dedekindle $p |- ( ( A C_ RR /\ B C_ RR /\ A. x e. A A. y e. B x <_ y ) ->
       E. z e. RR A. x e. A A. y e. B ( x <_ z /\ z <_ y ) ) $=
      ( vw cr wss cv cle wbr wral wa wi c0 wcel syl2an weq ssel2 ax-17 w3a wrex
      cin wceq clt simpr1 simpr2 wne wn simp1 simpl ra4 sylbi imp eleq1 biimpcd
      disj necon3bd ad2antll wb simp2 simp3 simpr ltlen syl11anc biimprd mpan2d
      mpd anassrs ralimdva 3imp2 dedekind syl111anc ex wex n0 inss1 sseli hbra1
      3exp hb3an hban hbral inss2 breq2 rcla4cv syl5 syl6 com23 imp32 3ad2antl3
      breq1 ralbidv rcla4cva r19.21bi jca ralrimi expr anbi12d 2ralbidv rcla4ev
      adantr expcom exlimiv pm2.61ine ) DGHZEGHZAIZBIZJKZBELZADLZUAZXHCIZJKZXNX
      IJKZMZBELADLZCGUBZNZDEUCZOYAOUDZXMXSYBXMMXFXGXHXIUEKZBELZADLZXSYBXFXGXLUF
      YBXFXGXLUGYBXFXGXLYEYBXFXGXLYENYBXFXGUAZXKYDADYFXHDPZMXJYCBEYFYGXIEPZXJYC
      NYFYGYHMZMZXJXIXHUHZYCYJXHEPZUIZYKYFYBYGYMYIYBXFXGUJYGYHUKZYBYGYMYBYMADLY
      GYMNADEUQYMADULUMUNQYHYMYKNYFYGYHYLXIXHBARYHYLXIXHEUOUPURUSVHYJYCXJYKMZYJ
      XHGPZXIGPZYCYOUTYFXFYGYPYIYBXFXGVAYNDGXHSQYFXGYHYQYIYBXFXGVBYGYHVCEGXISQX
      HXIVDVEVFVGVIVJVJVTVKABCDEVLVMVNYAOUHFIZYAPZFVOXTFYAVPYSXTFXMYSXSXMYSMZYR
      GPZXHYRJKZYRXIJKZMZBELZADLZXSXMXFYRDPZUUAYSXFXGXLUJYADYRDEVQVRZDGYRSQYTUU
      EADXMYSAXFXGXLAXFATXGATXKADVSWAYSATWBXMYSYGUUEXMYSYGMZMZUUDBEXMUUIBXFXGXL
      BXFBTXGBTXKBADYGBTXJBEVSWCWAUUIBTWBUUJYHUUDUUJYHMUUBUUCUUJUUBYHXLXFUUIUUB
      XGXLYSYGUUBXLYGYSUUBXLYGXKYSUUBNXKADULYSYREPXKUUBYAEYRDEWDVRXJUUBBYREXIYR
      XHJWEWFWGWHWIWJWKXBUUJUUCBEXMXLUUGUUCBELZUUIXFXGXLVBYSUUGYGUUHXBXKUUKAYRD
      AFRXJUUCBEXHYRXIJWLWMWNQWOWPVNWQWRWQXRUUFCYRGCFRZXQUUDABDEUULXOUUBXPUUCXN
      YRXHJWEXNYRXIJWLWSWTXAVEXCXDUMXE $.
      $( [2-Jul-2013] $)
  $}

  $( A generalized form of the cancellation law for multiplication.
     (Contributed by Scott Fenton, 17-Jun-2013.) $)
  mulcan1g $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
    ( ( A x. B ) = ( A x. C ) <-> ( A = 0 \/ B = C ) ) ) $=
    ( cc wcel w3a cmin co cmul cc0 wceq wo subdi eqeq1d wb simp1 3adant1 subeq0
    syl11anc axmulcl subcl mul0or orbi2d bitrd 3adant3 3adant2 3bitr3rd ) ADEZB
    DEZCDEZFZABCGHZIHZJKZABIHZACIHZGHZJKZAJKZBCKZLZUOUPKZUKUMUQJABCMNUKUNUSULJK
    ZLZVAUKUHULDEZUNVDOUHUIUJPUIUJVEUHBCUAQAULUBSUKVCUTUSUIUJVCUTOUHBCRQUCUDUKU
    ODEZUPDEZURVBOUHUIVFUJABTUEUHUJVGUIACTUFUOUPRSUG $.
    $( [17-Jun-2013] $)

  $( A generalized form of the cancellation law for multiplication.
     (Contributed by Scott Fenton, 17-Jun-2013.) $)
  mulcan2g $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
    ( ( A x. C ) = ( B x. C ) <-> ( A = B \/ C = 0 ) ) ) $=
    ( cc wcel w3a cmul co wceq wo mulcom 3adant2 3adant1 eqeq12d mulcan1g 3coml
    cc0 wb orcom syl6bb bitrd ) ADEZBDEZCDEZFZACGHZBCGHZICAGHZCBGHZIZABIZCQIZJZ
    UEUFUHUGUIUBUDUFUHIUCACKLUCUDUGUIIUBBCKMNUEUJULUKJZUMUDUBUCUJUNRCABOPULUKST
    UA $.
    $( [17-Jun-2013] $)

  $( Membership in the non-negative reals.  (Contributed by Scott Fenton,
     21-Jun-2013.) $)
  elrege0 $p |- ( A e. ( 0 [,) +oo ) <-> ( A e. RR /\ 0 <_ A ) ) $=
    ( cc0 cpnf cico co wcel cxr cle wbr clt w3a cr wa 0re rexr ax-mp pnfxr cmnf
    wb adantr elico1 mp2an xrrebnd 3ad2ant1 mnflt wi mnfxr xrltletr mp3an12 imp
    mpani 3adant3 simp3 mpbir2and simp2 jca simpr ltpnf 3jca impbii bitri ) ABC
    DEFZAGFZBAHIZACJIZKZALFZVDMZBGFZCGFVBVFSBLFZVINBOPZQBCAUAUBVFVHVFVGVDVFVGRA
    JIZVEVCVDVGVLVEMSVEAUCUDVCVDVLVEVCVDVLVCRBJIZVDVLVJVMNBUEPRGFVIVCVMVDMVLUFU
    GVKRBAUHUIUKUJULVCVDVEUMUNVCVDVEUOUPVHVCVDVEVGVCVDAOTVGVDUQVGVEVDAURTUSUTVA
    $.
    $( [21-Jun-2013] $)

  $( Swap the order of subtraction in a multiplication.  (Contributed by Scott
     Fenton, 24-Jun-2013.) $)
  mulsub2 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
    ( ( A - B ) x. ( C - D ) ) = ( ( B - A ) x. ( D - C ) ) ) $=
    ( cc wcel wa cmin cneg cmul wceq mul2neg syl2an negsubdi2 opreqan12d eqtr3d
    co subcl ) AEFBEFGZCEFDEFGZGABHQZIZCDHQZIZJQZUAUCJQZBAHQZDCHQZJQSUAEFUCEFUE
    UFKTABRCDRUAUCLMSTUBUGUDUHJABNCDNOP $.
    $( [24-Jun-2013] $)

  $( Swap the order of subtraction in a division.  (Contributed by Scott
     Fenton, 24-Jun-2013.) $)
  div2sub $p |- ( ( ( A e. CC /\ B e. CC ) /\
    ( C e. CC /\ D e. CC /\ C =/= D ) ) ->
    ( ( A - B ) / ( C - D ) ) = ( ( B - A ) / ( D - C ) ) ) $=
    ( cc wcel wa wne w3a cmin cneg cdiv cc0 wceq subcl 3adant3 subeq0 necon3bid
    co negsubdi2 biimprd 3impia jca div2neg 3expb syl2an opreqan12d eqtr3d ) AE
    FBEFGZCEFZDEFZCDHZIZGABJSZKZCDJSZKZLSZUNUPLSZBAJSZDCJSZLSUIUNEFZUPEFZUPMHZG
    URUSNZUMABOUMVCVDUJUKVCULCDOPUJUKULVDUJUKGZVDULVFUPMCDCDQRUAUBUCVBVCVDVEUNU
    PUDUEUFUIUMUOUTUQVALABTUJUKUQVANULCDTPUGUH $.
    $( [24-Jun-2013] $)

  $( A condition for multiplication to be non-negative.  (Contributed by Scott
     Fenton, 25-Jun-2013.) $)
  mulge0b $p |- ( ( A e. RR /\ B e. RR ) ->
    ( 0 <_ ( A x. B ) <->
      ( ( A <_ 0 /\ B <_ 0 ) \/ ( 0 <_ A /\ 0 <_ B ) ) ) ) $=
    ( cr wcel wa cc0 cmul co cle wbr wo wn wb 0re mpan adantr cdiv wceq syl2an
    wi ianor clt ltnle adantl orbi12d ltle imp ad2ant2rl axmulrcl simprl simpll
    simprr divge0 syl22anc cc mulcom opreq1d wne ad2antlr ad2antrr gt0ne0 div23
    recn syl112anc divcan1 syl111anc 3eqtrd jca expr simplr ad2ant2l eqtrd jaod
    breqtrd sylbird syl5bi orrd ex cneg renegcl mulge0 le0neg1 bi2anan9 mul2neg
    an4s breq2d bicomd 3imtr4d impbid ) ACDZBCDZEZFABGHZIJZAFIJZBFIJZEZFAIJZFBI
    JZEZKZWLWNXAWLWNEZWQWTWQLWOLZWPLZKZXBWTWOWPUAXBXEFAUBJZFBUBJZKZWTWLXHXEMWNW
    LXFXCXGXDWJXFXCMZWKFCDZWJXINFAUCOPWKXGXDMZWJXJWKXKNFBUCOUDUEPXBXFWTXGWLWNXF
    WTWLWNXFEZEZWRWSWJXFWRWKWNWJXFWRXJWJXFWRTNFAUFOUGUHXMFWMAQHZBIXMWMCDZWNWJXF
    FXNIJWLXOXLABUIZPWLWNXFUJWJWKXLUKWLWNXFULWMAUMUNXMXNBAGHZAQHZBAQHAGHZBXMWMX
    QAQWLWMXQRZXLWJAUODZBUODZXTWKAVCZBVCZABUPSPUQXMYBYAYAAFURZXRXSRWKYBWJXLYDUS
    ZWJYAWKXLYCUTZYGWJXFYEWKWNAVAUHZBAAVBVDXMYBYAYEXSBRYFYGYHBAVEVFVGVNVHVIWLWN
    XGWTWLWNXGEZEZWRWSYJFWMBQHZAIYJXOWNWKXGFYKIJWLXOYIXPPWLWNXGUJWJWKYIVJWLWNXG
    ULWMBUMUNYJYKABQHBGHZAYJYAYBYBBFURZYKYLRWJYAWKYIYCUTZWKYBWJYIYDUSZYOWKXGYMW
    JWNBVAVKZABBVBVDYJYAYBYMYLARYNYOYPABVEVFVLVNWKXGWSWJWNWKXGWSXJWKXGWSTNFBUFO
    UGVKVHVIVMVOVPVQVRWLWQWNWTWLFAVSZIJZFBVSZIJZEZFYQYSGHZIJZWQWNWJYQCDZYSCDZUU
    AUUCTWKAVTBVTUUDUUEEUUAUUCUUDYRUUEYTUUCYQYSWAWEVRSWJWOYRWKWPYTAWBBWBWCWLUUC
    WNWLUUBWMFIWJYAYBUUBWMRWKYCYDABWDSWFWGWHWLWTWNWJWRWKWSWNABWAWEVRVMWI $.
    $( [25-Jun-2013] $)


  $( A condition for multiplication to be non-positive.  (Contributed by Scott
     Fenton, 25-Jun-2013.) $)
  mulle0b $p |- ( ( A e. RR /\ B e. RR ) ->
    ( ( A x. B ) <_ 0 <->
      ( ( A <_ 0 /\ 0 <_ B ) \/ ( 0 <_ A /\ B <_ 0 ) ) ) ) $=
    ( cr wcel wa cmul co cc0 cle wbr cneg wo remulcl le0neg1 syl le0neg2 anbi2d
    wb cc recn orbi12d adantl renegcl mulge0b sylan2 mulneg2 breq2d 3bitr2rd
    syl2an bitrd ) ACDZBCDZEZABFGZHIJZHUNKZIJZAHIJZHBIJZEZHAIJZBHIJZEZLZUMUNCDU
    OUQRABMUNNOUMVDURBKZHIJZEZVAHVEIJZEZLZHAVEFGZIJZUQULVDVJRUKULUTVGVCVIULUSVF
    URBPQULVBVHVABNQUAUBULUKVECDVLVJRBUCAVEUDUEUKASDZBSDZVLUQRULATBTVMVNEVKUPHI
    ABUFUGUIUHUJ $.
    $( [25-Jun-2013] $)

  $( A condition for multiplication of subtraction to be non-positive.
     (Contributed by Scott Fenton, 25-Jun-2013.) $)
  mulsuble0b $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
    ( ( ( A - B ) x. ( C - B ) ) <_ 0 <->
      ( ( A <_ B /\ B <_ C ) \/ ( C <_ B /\ B <_ A ) ) ) ) $=
    ( cr wcel cmin co cc0 cle wbr wa wo wb resubcl 3adant3 ancoms suble0 subge0
    3adant1 anbi12d w3a cmul mulle0b syl11anc ancom syl6bb orbi12d bitrd ) ADEZ
    BDEZCDEZUAZABFGZCBFGZUBGHIJZUMHIJZHUNIJZKZHUMIJZUNHIJZKZLZABIJZBCIJZKZCBIJZ
    BAIJZKZLULUMDEZUNDEZUOVBMUIUJVIUKABNOUJUKVJUIUKUJVJCBNPSUMUNUCUDULURVEVAVHU
    LUPVCUQVDUIUJUPVCMUKABQOUJUKUQVDMZUIUKUJVKCBRPSTULVAVGVFKVHULUSVGUTVFUIUJUS
    VGMUKABROUJUKUTVFMZUIUKUJVLCBQPSTVGVFUEUFUGUH $.
    $( [25-Jun-2013] $)

  $( An interval law for less than or equal.  (Contributed by Scott Fenton,
     27-Jun-2013.) $)
  relin01 $p |- ( A e. RR ->
    ( A <_ 0 \/ ( 0 <_ A /\ A <_ 1 ) \/ 1 <_ A ) ) $=
    ( cr cc0 cle wbr c1 wa wo w3o 1re letric mpan2 pm3.21 orim2d syl5com orim1d
    wcel 0re mpd df-3or sylibr ) ABQZACDEZCADEZAFDEZGZHZFADEZHZUCUFUHIUBUEUHHZU
    IUBFBQUJJAFKLUBUEUGUHUBUCUDHZUEUGUBCBQUKRACKLUEUDUFUCUEUDMNOPSUCUFUHTUA $.
    $( [27-Jun-2013] $)

  $( The upper integers starting from a natural are a subset of the naturals.
     (Contributed by Scott Fenton, 29-Jun-2013.) $)
  uznnssnn $p |- ( N e. NN -> ( ZZ>= ` N ) C_ NN ) $=
    ( cn wcel cuz cfv c1 wss elnnuz uzss sylbi nnuz syl6sseqr ) ABCZADEZFDEZBMA
    OCNOGAHFAIJKL $.
    $( [29-Jun-2013] $)

  $( Bring a term in a subtraction into the numerator.  (Contributed by Scott
     Fenton, 3-Jul-2013.) $)
  subdivcomb1 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
     ( ( ( C x. A ) - B ) / C ) = ( A - ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cmin cdiv wceq simp3l simp1 mulcl syl11anc
    divsubdir syld3an1 divcan3 3expb 3adant2 opreq1d eqtrd ) ADEZBDEZCDEZCFGZHZ
    IZCAJKZBLKCMKZULCMKZBCMKZLKZAUOLKULDEZUGUFUJUMUPNUKUHUFUQUFUGUHUIOUFUGUJPCA
    QRULBCSTUKUNAUOLUFUJUNANZUGUFUHUIURACUAUBUCUDUE $.
    $( [3-Jul-2013] $)

  $( Bring a term in a subtraction into the numerator.  (Contributed by Scott
     Fenton, 3-Jul-2013.) $)
  subdivcomb2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
     ( ( A - ( C x. B ) ) / C ) = ( ( A / C ) - B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cmin cdiv wceq simp3l simp2 mulcl syl11anc
    c1 eqtrd divsubdir syld3an2 simprl simpl simpr div23 syl111anc divid mulid2
    opreq1d sylan9eqr 3adant1 opreq2d ) ADEZBDEZCDEZCFGZHZIZACBJKZLKCMKZACMKZUT
    CMKZLKZVBBLKUNUTDEZUOURVAVDNUSUPUOVEUNUOUPUQOUNUOURPCBQRAUTCUAUBUSVCBVBLUOU
    RVCBNUNUOURHZVCCCMKZBJKZBVFUPUOURVCVHNUOUPUQUCUOURUDUOURUECBCUFUGURUOVHSBJK
    BURVGSBJCUHUJBUIUKTULUMT $.
    $( [3-Jul-2013] $)

  $( Relationship between subtraction and addition.  (Contributed by Scott
     Fenton, 5-Jul-2013.) $)
  subadd2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
            ( ( A - B ) = C <-> ( C + B ) = A ) ) $=
    ( cc wcel cmin co caddc wb c1 cif opreq1 eqeq1d eqeq2 bibi12d opreq2 ax-1cn
    wceq elimel subadd2i dedth3h ) ADEZBDEZCDEZABFGZCRZCBHGZARZIUBAJKZBFGZCRZUG
    UIRZIUIUCBJKZFGZCRZCUMHGZUIRZIUNUDCJKZRZURUMHGZUIRZIABCJJJAUIRZUFUKUHULVBUE
    UJCAUIBFLMAUIUGNOBUMRZUKUOULUQVCUJUNCBUMUIFPMVCUGUPUIBUMCHPMOCURRZUOUSUQVAC
    URUNNVDUPUTUICURUMHLMOUIUMURAJDQSBJDQSCJDQSTUA $.
    $( [5-Jul-2013] $)

  $( Reverse the order of subtraction in an equality.  (Contributed by Scott
     Fenton, 8-Jul-2013.) $)
  subeqrev $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
    ( ( A - B ) = ( C - D ) <-> ( B - A ) = ( D - C ) ) ) $=
    ( cc wcel wa cmin co cneg wb subcl neg11 syl2an negsubdi2 eqeqan12d bitr3d
    wceq ) AEFBEFGZCEFDEFGZGABHIZJZCDHIZJZRZUAUCRZBAHIZDCHIZRSUAEFUCEFUEUFKTABL
    CDLUAUCMNSTUBUGUDUHABOCDOPQ $.
    $( [8-Jul-2013] $)

  $( Shift membership in a finite sequence of naturals.  (Contributed by Scott
     Fenton, 17-Jul-2013.) $)
  fznatpl1 $p |- ( ( N e. NN /\ I e. ( 1 ... ( N - 1 ) ) ) ->
           ( I + 1 ) e. ( 1 ... N ) ) $=
    ( wcel c1 co cfz wa caddc cle wbr cz adantl mp3an2 syl11anc clt zre syl 1re
    wb cr cn cmin elfzelz peano2zdi nnz adantr 1z elfz peano2re a1i ltp1 leadd1
    elfzle1 mp3an13 ltletrd wi ltle mpan sylc elfzle2 leaddsub mpbird mpbir2and
    mpbid ) BUACZADBDUBEZFECZGZADHEZDBFECZDVIIJZVIBIJZVHVIKCZBKCZVJVKVLGSZVGVMV
    EVGAADVFUCZUDLVEVNVGBUEUFZVMDKCVNVOUGVIDBUHMNVHVITCZDVIOJZVKVHATCZVRVGVTVEV
    GAKCVTVPAPQLZAUIQZVHDDDHEZVIDTCZVHRUJZVHWDWCTCWEDUIQWBVHWDDWCOJWEDUKQVHDAIJ
    ZWCVIIJZVGWFVEADVFUMLVHVTWFWGSZWAWDVTWDWHRRDADULUNQVDUOWDVRVSVKUPRDVIUQURUS
    VHVLAVFIJZVGWIVEADVFUTLVHVTBTCZVLWISZWAVHVNWJVQBPQVTWDWJWKRADBVAMNVBVC $.
    $( [17-Jul-2013] $)


  ${
    $d N x $.
    ltweuzlem1.1 $e |- N e. ZZ $.
    $( Lemma for ~ ltweuz .  Less than well orders a set of upper integers
       given that the argument to ` ZZ>= ` is an integer. $)
    ltweuzlem1 $p |- < We ( ZZ>= ` N ) $=
      ( ltfrn ) AC $.
      $( [6-Aug-2013] $)
  $}

  $( Less than well orders a set of upper integers.  (Contributed by Scott
     Fenton, 8-Aug-2013.) $)
  ltweuz $p |- < We ( ZZ>= ` N ) $=
    ( cz wcel cuz cfv clt wwe c1 cif wb fveq2 weeq2 syl elimel ltweuzlem1 dedth
    wceq 1z wn c0 we0 cdm cpw fdmi eleq2i notbii ndmfv sylbir mpbiri pm2.61i
    uzf ) ABCZADEZFGZULUNULAHIZDEZFGZAHAUOQUMUPQUNUQJAUODKUMUPFLMUOAHBRNOPULSZU
    NTFGZFUAURUMTQZUNUSJURADUBZCZSUTVBULVABABBUCDUKUDUEUFADUGUHUMTFLMUIUJ $.
    $( [8-Aug-2013] $)

  $( Less than well orders the naturals.  (Contributed by Scott Fenton,
     6-Aug-2013.) $)
  ltwenn $p |- < We NN $=
    ( cn clt wwe c1 cuz cfv ltweuz wceq wb nnuz weeq2 ax-mp mpbir ) ABCZDEFZBCZ
    DGAOHNPIJAOBKLM $.
    $( [6-Aug-2013] $)

  $( Less than well orders a set of finite integers.  (Contributed by Scott
     Fenton, 8-Aug-2013.) $)
  ltwefz $p |- < We ( M ... N ) $=
    ( cfz co cuz cfv wss clt wwe fzssuz ltweuz wess mp2 ) ABCDZAEFZGOHINHIABJAK
    NOHLM $.
    $( [8-Aug-2013] $)

  ${
    $d N x $.  $d M x $.
    $( The supremum of a finite sequence of integers.  (Contributed by Scott
       Fenton, 8-Aug-2013.) $)
    supfz $p |- ( N e. ( ZZ>= ` M ) -> sup ( ( M ... N ) , ZZ , < ) = N ) $=
      ( vx cuz cfv wcel cz cfz co cv clt wbr wn wral csup wceq cr zre syl wor
      eluzelz eluzfz2 wa elfzle2 adantl wb elfzelz lenlt syl2an mpbid ralrimiva
      cle ancoms wss zssre ltso soss mp2 supmax syl111anc ) BADEFZBGFZBABHIZFBC
      JZKLMZCVCNVCGKOBPABUAZABUBVAVECVCVAVDVCFZUCVDBULLZVEVGVHVAVDABUDUEVGVAVHV
      EUFZVGVDQFZBQFZVIVAVGVDGFVJVDABUGVDRSVAVBVKVFBRSVDBUHUIUMUJUKCGVCBKGQUNQK
      TGKTUOUPGQKUQURUSUT $.
      $( [8-Aug-2013] $)

    $( The infimum of a finite sequence of integers.  (Contributed by Scott
       Fenton, 8-Aug-2013.) $)
    inffz $p |- ( N e. ( ZZ>= ` M ) -> sup ( ( M ... N ) , ZZ , `' < ) = M ) $=
      ( vx cuz cfv wcel cz cfz co cv clt ccnv wbr wn wa cr wb zre syl wor lenlt
      wral csup wceq eluzel2 eluzfz1 elfzle1 adantl elfzelz syl2an mpbid brcnvg
      cle notbid sylan mpbird ralrimiva wss zssre ltso soss mp2 cnvso syl111anc
      mpbi supmax ) BADEFZAGFZAABHIZFACJZKLZMZNZCVIUBVIGVKUCAUDABUEZABUFVGVMCVI
      VGVJVIFZOZVMVJAKMZNZVPAVJUMMZVRVOVSVGVJABUGUHVGAPFZVJPFZVSVRQVOVGVHVTVNAR
      SVOVJGFWAVJABUIVJRSAVJUAUJUKVGVHVOVMVRQVNVHVOOVLVQAVJGVIKULUNUOUPUQCGVIAV
      KGKTZGVKTGPURPKTWBUSUTGPKVAVBGKVCVEVFVD $.
      $( [8-Aug-2013] $)

  $}

  $( The naturals are closed under squaring.  (Contributed by Scott Fenton,
     29-Mar-2014.) $)
  nnsqcl $p |- ( A e. NN -> ( A ^ 2 ) e. NN ) $=
    ( cn wcel c2 cexp co cmul cc wceq nncn sqval syl nnmulcl anidms eqeltrd ) A
    BCZADEFZAAGFZBPAHCQRIAJAKLPRBCAAMNO $.
    $( [19-Apr-2014] $) $( [30-Mar-2014] $)


  $( Square root is strictly monotonic.  Closed form of ~ sqrlti .
     (Contributed by Scott Fenton, 17-Apr-2014.) $)
  sqrlt $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
    ( A < B <-> ( sqr ` A ) < ( sqr ` B ) ) ) $=
    ( cr wcel cc0 cle wbr clt csqr cfv wb wa wi cif breq2 fveq2 bibi12d imbi12d
    wceq 0re anbi1d breq1 breq1d anbi2d breq2d elimel sqrlti dedth2h imp an4s )
    ACDZBCDZEAFGZEBFGZABHGZAIJZBIJZHGZKZUKULLUMUNLZUSUKULUTUSMEUKAENZFGZUNLZVAB
    HGZVAIJZUQHGZKZMVBEULBENZFGZLZVAVHHGZVEVHIJZHGZKZMABEEAVASZUTVCUSVGVOUMVBUN
    AVAEFOUAVOUOVDURVFAVABHUBVOUPVEUQHAVAIPUCQRBVHSZVCVJVGVNVPUNVIVBBVHEFOUDVPV
    DVKVFVMBVHVAHOVPUQVLVEHBVHIPUEQRVAVHAECTUFBECTUFUGUHUIUJ $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  $( Integers are closed under squaring.  (Contributed by Scott Fenton,
     18-Apr-2014.) $)
  zsqcl $p |- ( A e. ZZ -> ( A ^ 2 ) e. ZZ ) $=
    ( cz wcel c2 cn0 cexp co 2nn0 zexpcl mpan2 ) ABCDECADFGBCHADIJ $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)


  $( Square can be moved in and out of absolute value.  (Contributed by Scott
     Fenton, 18-Apr-2014.) $)
  abssq $p |- ( A e. CC -> ( ( abs ` A ) ^ 2 ) = ( abs ` ( A ^ 2 ) ) ) $=
    ( cc wcel cmul co cabs cfv cexp wceq absmul anidms sqval fveq2d abscl recnd
    c2 syl 3eqtr4rd ) ABCZAADEZFGZAFGZUBDEZAPHEZFGUBPHEZSUAUCIAAJKSUDTFALMSUBBC
    UEUCISUBANOUBLQR $.
    $( [19-Apr-2014] $) $( [18-Apr-2014] $)

  $( Subtraction of two ratios.  (Contributed by Scott Fenton, 22-Apr-2014.) $)
  divsubdiv $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( ( C e. CC /\ C =/= 0 )
                    /\ ( D e. CC /\ D =/= 0 ) ) ) ->
  ( ( A / C ) - ( B / D ) ) = ( ( ( A x. D ) - ( C x. B ) ) / ( C x. D ) ) ) $=
    ( cc wcel wa cc0 wne cdiv cneg caddc cmul cmin wceq syl111anc opreq2d divcl
    co syl11anc divadddiv sylanl2 simplr simprrl simprrr divneg simprll simprlr
    negcl simpll negsub eqtr3d mulneg2 mulcl eqtrd opreq1d 3eqtr3d ) AEFZBEFZGZ
    CEFZCHIZGZDEFZDHIZGZGZGZACJSZBKZDJSZLSZADMSZCVJMSZLSZCDMSZJSZVIBDJSZNSZVMCB
    MSZNSZVPJSUSURVJEFVGVLVQOBUIAVJCDUAUBVHVIVRKZLSZVLVSVHWBVKVILVHUSVDVEWBVKOU
    RUSVGUCZUTVCVDVEUDZUTVCVDVEUEZBDUFPQVHVIEFZVREFZWCVSOVHURVAVBWGURUSVGUJZUTV
    AVBVFUGZUTVAVBVFUHACRPVHUSVDVEWHWDWEWFBDRPVIVRUKTULVHVOWAVPJVHVOVMVTKZLSZWA
    VHVNWKVMLVHVAUSVNWKOWJWDCBUMTQVHVMEFZVTEFZWLWAOVHURVDWMWIWEADUNTVHVAUSWNWJW
    DCBUNTVMVTUKTUOUPUQ $.
    $( [22-Apr-2014] $)

  ${
    $d B k $.  $d M k $.  $d N k $.
    fsumsplitlast.1 $e |- ( k = ( N + 1 ) -> A = B ) $.
    $( Split off the last term of a finite sum.  (Contributed by Scott Fenton,
       22-Apr-2014.) $)
    fsumsplitlast $p |- (
       ( N e. ( ZZ>= ` M ) /\ A. k e. ( M ... ( N + 1 ) ) A e. CC ) ->
       sum_ k e. ( M ... ( N + 1 ) ) A =
       ( sum_ k e. ( M ... N ) A + B ) ) $=
      ( cuz wcel cc c1 caddc co cfz csu cz wceq adantr eluzfz2 syl opreq2d wral
      cfv wa cmin eluzelz peano2zdi zcn ax-1cn pncan sylancl eleqtrrd fsumsplit
      simpr syl111anc peano2uz cv eleq1d rcla4cv mpan9 fsum1i syl11anc eqtrd )
      EDGUBZHZAIHZCDEJKLZMLZUAZUCZVGACNZDEMLZACNZVFVFMLACNZKLZVLBKLVIVFOHZEDVFJ
      UDLZMLZHZVHVJVNPVDVOVHVDEDEUEZUFQZVDVRVHVDEVKVQDERVDVPEDMVDEIHZJIHVPEPVDE
      OHWAVSEUGSUHEJUIUJTUKQVDVHUMACEDVFULUNVIVMBVLKVIBIHZVOVMBPVDVFVGHZVHWBVDV
      FVCHWCDEUODVFRSVEWBCVFVGCUPVFPABIFUQURUSVTABICVFFUTVATVB $.
      $( [22-Apr-2014] $)
  $}

  ${
    $d F i $.  $d F j $.  $d F k $.  $d i j $.  $d i k $.  $d j k $.  $d M i $.
    $d M j $.  $d M k $.  $d N j $.  $d N k $.
    $( Closed form value of a telescoping series.  (Contributed by Scott
       Fenton, 24-Apr-2014.) $)
    fsumtelescope $p |- ( ( N e. ( ZZ>= ` M ) /\
      A. k e. ( M ... ( N + 1 ) ) ( F ` k ) e. CC ) ->
      sum_ k e. ( M ... N ) ( ( F ` k ) - ( F ` ( k + 1 ) ) ) =
      ( ( F ` M ) - ( F ` ( N + 1 ) ) ) ) $=
      ( vj cfv wcel cc c1 caddc co cfz wral cmin wceq opreq1 opreq2d fveq2d cvv
      wi vi cuz cv csu raleqdv opreq2 sumeq1d eqeq12d imbi12d cz oprex opreq12d
      weq fveq2 fsum1i mpan a1d wa wss fzssp1 ssralv pm2.27 syl adantl peano2uz
      mp2b eluzfz1 eleq1d rcla4v visset eluzfz2 fzp1elp1 sylancr fzelp1i npncan
      imp syl111anc eqeq1d syl5ibrcom simplr sylc subcl syl11anc cbvralv sylibr
      ralrimiva fsumsplitlast syldan sylibrd syld ex com23 uzind4 ) DCUBFZGAUCZ
      BFZHGZACDIJKZLKZMZCDLKZWPWOIJKZBFZNKZAUDZCBFZWRBFZNKZOZWQACEUCZIJKZLKZMZC
      XJLKZXDAUDZXFXKBFZNKZOZTWQACCIJKZLKZMZCCLKZXDAUDZXFXSBFZNKZOZTWQACUAUCZIJ
      KZLKZMZCYGLKZXDAUDZXFYHBFZNKZOZTZWQACYHIJKZLKZMZYIXDAUDZXFYQBFZNKZOZTWTXI
      TEUACDXJCOZXMYAXRYFUUDWQAXLXTUUDXKXSCLXJCIJPZQUEUUDXOYCXQYEUUDXNYBXDAXJCC
      LUFUGUUDXPYDXFNUUDXKXSBUUERQUHUIEUAUMZXMYJXRYOUUFWQAXLYIUUFXKYHCLXJYGIJPZ
      QUEUUFXOYLXQYNUUFXNYKXDAXJYGCLUFUGUUFXPYMXFNUUFXKYHBUUGRQUHUIXJYHOZXMYSXR
      UUCUUHWQAXLYRUUHXKYQCLXJYHIJPZQUEUUHXOYTXQUUBUUHXNYIXDAXJYHCLUFUGUUHXPUUA
      XFNUUHXKYQBUUIRQUHUIXJDOZXMWTXRXIUUJWQAXLWSUUJXKWRCLXJDIJPZQUEUUJXOXEXQXH
      UUJXNXAXDAXJDCLUFUGUUJXPXGXFNUUJXKWRBUUKRQUHUICUJGZYFYAYESGUULYFXFYDNUKXD
      YESACWOCOZWPXFXCYDNWOCBUNZUUMXBXSBWOCIJPRULUOUPUQYGWNGZYSYPUUCUUOYSYPUUCT
      UUOYSURZYPYOUUCYSYPYOTZUUOYSYJUUQYHSGZYIYRUSYSYJTYGIJUKZCYHSUTWQAYIYRVAVF
      YJYOVBVCVDUUPYOYLYMUUANKZJKZUUBOZUUCUUPUVBYOYNUUTJKZUUBOZUUPXFHGZYMHGZUUA
      HGZUVDUUOYSUVEUUOCYRGZYSUVETUUOYQWNGZUVHUUOYHWNGUVICYGVECYHVEVCCYQVGVCWQU
      VEACYRUUMWPXFHUUNVHVIVCVPUUOYSUVFUUOYHYRGZYSUVFTUUOYHYIGZUVJUUOYGSGYGYKGU
      VKUAVJCYGVKSYGCYGVLVMZYHCYHUUSVNVCWQUVFAYHYRWOYHOZWPYMHWOYHBUNZVHVIVCVPUU
      OYSUVGUUOYQYRGZYSUVGTUUOUURUVKUVOUUSUVLSYHCYHVLVMWQUVGAYQYRWOYQOWPUUAHWOY
      QBUNVHVIVCVPXFYMUUAVOVQYOUVAUVCUUBYLYNUUTJPVRVSUUPYTUVAUUBUUOYSXDHGZAYIMZ
      YTUVAOUUPXJBFZXPNKZHGZEYIMUVQUUPUVTEYIUUPXJYIGZURZUVRHGZXPHGZUVTUWBXJYRGZ
      YSUWCUWAUWEUUPXJCYHUUSVNVDUUOYSUWAVTZWQUWCAXJYRAEUMZWPUVRHWOXJBUNZVHVIWAU
      WBXKYRGZYSUWDUWAUWIUUPUURUWAUWIUUSSXJCYHVLUPVDUWFWQUWDAXKYRWOXKOWPXPHWOXK
      BUNVHVIWAUVRXPWBWCWFUVPUVTAEYIUWGXDUVSHUWGWPUVRXCXPNUWHUWGXBXKBWOXJIJPRUL
      VHWDWEXDUUTACYGUVMWPYMXCUUANUVNUVMXBYQBWOYHIJPRULWGWHVRWIWJWKWLWMVP $.
      $( [24-Apr-2014] $)
  $}

  ${
    $d A k $.  $d B n $.  $d C n $.  $d D n $.  $d F n $.  $d k n $.  $d M k $.
    $d M n $.  $d N k $.  $d N n $.
    fsumtelescope2.1 $e |- ( n = k -> A = B ) $.
    fsumtelescope2.2 $e |- ( n = ( k + 1 ) -> A = C ) $.
    fsumtelescope2.3 $e |- ( n = M -> A = D ) $.
    fsumtelescope2.4 $e |- ( n = ( N + 1 ) -> A = F ) $.
    $( Alternate form of ~ fsumtelescope with implicit substitutions.
       (Contributed by Scott Fenton, 25-Apr-2014.) $)
    fsumtelescope2 $p |- ( ( N e. ( ZZ>= ` M ) /\
       A. n e. ( M ... ( N + 1 ) ) A e. CC ) ->
       sum_ k e. ( M ... N ) ( B - C ) = ( D - F ) ) $=
      ( cfv wcel cc co cmin wceq syl11anc cuz c1 caddc cfz wral wa cv csu simpr
      cmpt wi weq eleq1d rcla4cv adantl imp eqid fvmptg ralrimiva fsumtelescope
      eqeltrd syldan fzelp1 adantlr ad2antlr fzp1elp1 opreq12d sumeq2dv eluzfz1
      mpd peano2uz syl adantr mpan9 eluzfz2 3eqtr3d ) IHUANZOZAPOZFHIUBUCQZUDQZ
      UEZUFZHIUDQZEUGZFWAAUJZNZWEUBUCQZWFNZRQZEUHZHWFNZVTWFNZRQZWDBCRQZEUHDGRQV
      RWBWGPOZEWAUEWKWNSWCWPEWAWCWEWAOZUFZWGBPWRWQBPOZWGBSZWCWQUIWCWQWSWBWQWSUK
      ZVRVSWSFWEWAFEULABPJUMUNZUOUPZFWEABWAPWFJWFUQZURZTXCVAUSEWFHIUTVBWCWDWJWO
      EWCWEWDOZUFZWGBWICRXGWQWSWTVRXFWQWBVQWEHIVCVDZXGWQWSXHWBXAVRXFXBVEVJXETXG
      WHWAOZCPOZWICSVRXFXIWBVQWEHIVFVDZXGXIXJXKWBXIXJUKVRXFVSXJFWHWAFUGZWHSACPK
      UMUNVEVJFWHACWAPWFKXDURTVGVHWCWLDWMGRWCHWAOZDPOZWLDSVRXMWBVRVTVQOZXMHIVKZ
      HVTVIVLZVMVRXMWBXNXQVSXNFHWAXLHSADPLUMUNVNFHADWAPWFLXDURTWCVTWAOZGPOZWMGS
      VRXRWBVRXOXRXPHVTVOVLZVMVRXRWBXSXTVSXSFVTWAXLVTSAGPMUMUNVNFVTAGWAPWFMXDUR
      TVGVP $.
      $( [25-Apr-2014] $)
  $}

