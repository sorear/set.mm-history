$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Properties of reals and complexes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    elicc2i.1 $e |- A e. RR $.
    elicc2i.2 $e |- B e. RR $.
    $( Inference for membership in a closed interval.  (Contributed by Scott
       Fenton, 3-Jun-2013.) $)
    elicc2i $p |- ( C e. ( A [,] B ) <-> ( C e. RR /\ A <_ C /\ C <_ B ) ) $=
      ( cr wcel cicc co cle wbr w3a wb elicc2 mp2an ) AFGBFGCABHIGCFGACJKCBJKLM
      DEABCNO $.
      $( [16-Sep-2013] $) $( [3-Jun-2013] $)
  $}

  ${
    sqdivzi.1 $e |- A e. CC $.
    sqdivzi.2 $e |- B e. CC $.
    $( Distribution of square over division.  (Contributed by Scott Fenton,
       7-Jun-2013.) $)
    sqdivzi $p |- ( B =/= 0 ->
            ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) ) $=
      ( cc0 wne cdiv co c2 cexp wceq c1 cif opreq2 opreq1d opreq1 opreq2d
      eqeq12d cc ax1cn keepel elimne0 sqdivi dedth ) BEFZABGHZIJHZAIJHZBIJHZGHZ
      KAUEBLMZGHZIJHZUHUKIJHZGHZKBLBUKKZUGUMUJUOUPUFULIJBUKAGNOUPUIUNUHGBUKIJPQ
      RAUKCUEBLSDTUABUBUCUD $.
      $( [16-Sep-2013] $) $( [7-Jun-2013] $)
  $}

  $( Distribution of square over division.  (Moved into main set.mm as ~ sqdiv
     and may be deleted by mathbox owner, SF. --NM 16-Jun-2013.) $)
  sqdivOLD $p |- ( ( A e. CC /\ B e. CC /\ B =/= 0 ) ->
      ( ( A / B ) ^ 2 ) = ( ( A ^ 2 ) / ( B ^ 2 ) ) ) $=
    ( sqdiv ) ABC $.
    $( [16-Sep-2013] $) $( [7-Jun-2013] $)

  $( Cancellation law for division and multiplication.  (Contributed by Scott
     Fenton, 7-Jun-2013.) $)
  dmdcan $p |- ( ( ( A e. CC /\ A =/= 0 ) /\ ( B e. CC /\ B =/= 0 ) /\
         C e. CC ) ->
         ( ( A / B ) x. ( C / A ) ) = ( C / B ) ) $=
    ( cc wcel cc0 wne wa w3a cdiv co cmul c1 wceq simp1l simp3 mulcom syl11anc
    opreq1d simp2l simp2r divass syl112anc 3eqtr3rd divcl syl111anc simp1r
    div23 eqtrd 3eqtr3d divid 3ad2ant1 mulid2 syl 3eqtrd ) ADEZAFGZHZBDEZBFGZHZ
    CDEZIZABJKZCAJKZLKZAAJKZCBJKZLKZMVHLKZVHVCCVDLKZAJKZAVHLKZAJKZVFVIVCVKVMAJV
    CACLKZBJKZCALKZBJKZVMVKVCVOVQBJVCUPVBVOVQNUPUQVAVBOZURVAVBPZACQRSVCUPVBUSUT
    VPVMNVSVTURUSUTVBTZURUSUTVBUAZACBUBUCVCVBUPUSUTVRVKNVTVSWAWBCABUBUCUDSVCVLV
    EVDLKZVFVCVBVDDEZUPUQVLWCNVTVCUPUSUTWDVSWAWBABUEUFZVSUPUQVAVBUGZCVDAUHUCVCV
    EDEZWDWCVFNVCVBUPUQWGVTVSWFCAUEUFWEVEVDQRUIVCUPVHDEZUPUQVNVINVSVCVBUSUTWHVT
    WAWBCBUEUFZVSWFAVHAUHUCUJURVAVIVJNVBURVGMVHLAUKSULVCWHVJVHNWIVHUMUNUO $.
    $( [16-Sep-2013] $) $( [7-Jun-2013] $)

  $( Swap the order of subtraction in a square.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  sqsubswap $p |- ( ( A e. CC /\ B e. CC ) ->
             ( ( A - B ) ^ 2 ) = ( ( B - A ) ^ 2 ) ) $=
    ( cc wcel wa cmin co cneg c2 cexp wceq subcl sqneg syl negsubdi2 opreq1d
    eqtr3d ) ACDBCDEZABFGZHZIJGZSIJGZBAFGZIJGRSCDUAUBKABLSMNRTUCIJABOPQ $.
    $( [16-Sep-2013] $) $( [10-Jun-2013] $)

  $( Expand the square of a subtraction.  (Contributed by Scott Fenton,
     10-Jun-2013.) $)
  binom2sub $p |- ( ( A e. CC /\ B e. CC ) ->
    ( ( A - B ) ^ 2 ) =
    ( ( ( A ^ 2 ) - ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) ) $=
    ( cc wcel wa cneg caddc co c2 cexp cmul cmin wceq negcl binom2 sylan2
    negsub opreq1d mulneg2 opreq2d 2cn axmulcl sylancr eqtr2d sqcl adantr
    mulcl syl11anc eqtr3d sqneg adantl opreq12d 3eqtr3d ) ACDZBCDZEZABFZGHZIJHZ
    AIJHZIAUQKHZKHZGHZUQIJHZGHZABLHZIJHUTIABKHZKHZLHZBIJHZGHUOUNUQCDUSVEMBNAUQO
    PUPURVFIJABQRUPVCVIVDVJGUPUTVHFZGHZVCVIUPVKVBUTGUPVBIVGFZKHZVKUPVAVMIKABSTU
    PICDZVGCDZVNVKMUAABUBZIVGSUCUDTUPUTCDZVHCDZVLVIMUNVRUOAUEUFUPVOVPVSUAVQIVGU
    GUCUTVHQUHUIUOVDVJMUNBUJUKULUM $.
    $( [16-Sep-2013] $) $( [10-Jun-2013] $)

  ${
    binom2subi.1 $e |- A e. CC $.
    binom2subi.2 $e |- B e. CC $.
    $( Expand the square of a subtraction.  (Contributed by Scott Fenton,
       13-Jun-2013.) $)
    binom2subi $p |- ( ( A - B ) ^ 2 ) =
    ( ( ( A ^ 2 ) - ( 2 x. ( A x. B ) ) ) + ( B ^ 2 ) ) $=
      ( cc wcel cmin co c2 cexp cmul caddc wceq binom2sub mp2an ) AEFBEFABGHIJH
      AIJHIABKHKHGHBIJHLHMCDABNO $.
      $( [16-Sep-2013] $) $( [13-Jun-2013] $)
  $}

  $( Zero is an element of the closed unit.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  0elunit $p |- 0 e. ( 0 [,] 1 ) $=
    ( cc0 c1 cicc co wcel cr cle wbr 0re 1re elicc2i leidi lt01 ltleii
    mpbir3an ) AABCDEAFEAAGHABGHABAIJKIAILABIJMNO $.
    $( [16-Sep-2013] $) $( [11-Jun-2013] $)

  $( One is an element of the closed unit.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  1elunit $p |- 1 e. ( 0 [,] 1 ) $=
    ( c1 cc0 cicc co wcel cr cle wbr 0re 1re elicc2i lt01 ltleii leidi
    mpbir3an ) ABACDEAFEBAGHAAGHBAAIJKJBAIJLMAJNO $.
    $( [16-Sep-2013] $) $( [11-Jun-2013] $)

  $( A condition for a ratio to be a member of the closed unit.  (Contributed
     by Scott Fenton, 11-Jun-2013.) $)
  divelunit $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 < B ) ) ->
      ( ( A / B ) e. ( 0 [,] 1 ) <-> A <_ B ) ) $=
    ( cdiv co cc0 c1 cicc wcel cr cle wbr wa clt w3a 0re 1re elicc2i df-3an
    bitri cmul wb ledivmul mp3an2 adantlr wne simpll simprl gt0ne0 adantl
    redivcl syl111anc divge0 jca biantrurd cc wceq recn ad2antrl mulid1 syl
    breq2d 3bitr3d syl5bb ) ABCDZEFGDHZVDIHZEVDJKZLZVDFJKZLZAIHZEAJKZLZBIHZEBMK
    ZLZLZABJKZVEVFVGVINVJEFVDOPQVFVGVIRSVQVIABFTDZJKZVJVRVKVPVIVTUAZVLVKFIHVPWA
    PAFBUBUCUDVQVHVIVQVFVGVQVKVNBEUEZVFVKVLVPUFVMVNVOUGVPWBVMBUHUIABUJUKABULUMU
    NVQVSBAJVQBUOHZVSBUPVNWCVMVOBUQURBUSUTVAVBVC $.
    $( [16-Sep-2013] $) $( [11-Jun-2013] $)

  $( The square root function is one to one.  (Contributed by Scott Fenton,
     11-Jun-2013.) $)
  sqr11 $p |- ( ( ( A e. RR /\ 0 <_ A ) /\ ( B e. RR /\ 0 <_ B ) ) ->
    ( ( sqr ` A ) = ( sqr ` B ) <-> A = B ) ) $=
    ( cr wcel cc0 cle wbr csqr cfv wceq wb wa wi cif breq2 anbi1d fveq2 eqeq1d
    eqeq1 bibi12d imbi12d anbi2d eqeq2d eqeq2 0re elimel sqr11i dedth2h imp
    an4s ) ACDZBCDZEAFGZEBFGZAHIZBHIZJZABJZKZUKULLUMUNLZUSUKULUTUSMEUKAENZFGZUN
    LZVAHIZUPJZVABJZKZMVBEULBENZFGZLZVDVHHIZJZVAVHJZKZMABEEAVAJZUTVCUSVGVOUMVBU
    NAVAEFOPVOUQVEURVFVOUOVDUPAVAHQRAVABSTUABVHJZVCVJVGVNVPUNVIVBBVHEFOUBVPVEVL
    VFVMVPUPVKVDBVHHQUCBVHVAUDTUAVAVHAECUEUFBECUEUFUGUHUIUJ $.
    $( [16-Sep-2013] $) $( [11-Jun-2013] $)

  ${
    $d N k $.  $d M k $.
    $( Negation of a finite sum.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    fsumneg $p |- ( ( N e. ( ZZ>= ` M ) /\
    A. k e. ( M ... N ) A e. CC ) ->
    sum_ k e. ( M ... N ) -u A = -u sum_ k e. ( M ... N ) A ) $=
      ( cuz cfv wcel cc cfz co wral wa c1 cneg csu cmul wceq ax1cn negcli
      fsummulc1 mp3an2 fsumcl mulm1 syl ralimi sumeq2d adantl 3eqtr3rd ) DCEFGZ
      AHGZBCDIJZKZLZMNZUKABOZPJZUKUNAPJZBOZUONZUKANZBOZUIUNHGULUPURQMRSAUNBCDTU
      AUMUOHGUPUSQABCDUBUOUCUDULURVAQUIULUKUQUTBUJUQUTQBUKAUCUEUFUGUH $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  ${
    $d N k $.  $d M k $.
    $( Split a finite sum over a subtraction.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    fsumsub $p |- ( ( N e. ( ZZ>= ` M ) /\
    A. k e. ( M ... N ) ( A e. CC /\ B e. CC ) ) ->
    sum_ k e. ( M ... N ) ( A - B ) =
    ( sum_ k e. ( M ... N ) A - sum_ k e. ( M ... N ) B ) ) $=
      ( cuz cfv wcel cc wa cfz co wral cneg caddc csu cmin wceq negcl anim2i
      ralimi fsumadd sylan2 negsub sumeq2d adantl simpr fsumneg opreq2d simpl
      fsumcl syl11anc eqtrd 3eqtr3d ) EDFGHZAIHZBIHZJZCDEKLZMZJZUSABNZOLZCPZUSA
      CPZUSVBCPZOLZUSABQLZCPZVEUSBCPZQLZUTUOUPVBIHZJZCUSMVDVGRURVMCUSUQVLUPBSTU
      AAVBCDEUBUCUTVDVIRUOUTUSVCVHCURVCVHRCUSABUDUAUEUFVAVGVEVJNZOLZVKVAVFVNVEO
      UTUOUQCUSMZVFVNRURUQCUSUPUQUGUAZBCDEUHUCUIVAVEIHZVJIHZVOVKRUTUOUPCUSMVRUR
      UPCUSUPUQUJUAACDEUKUCUTUOVPVSVQBCDEUKUCVEVJUDULUMUN $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  ${
    pm2.61iine.1 $e |- ( ( A =/= C /\ B =/= D ) -> ph ) $.
    pm2.61iine.2 $e |- ( A = C -> ph ) $.
    pm2.61iine.3 $e |- ( B = D -> ph ) $.
    $( Equality version of ~ pm2.61ii .  (Contributed by Scott Fenton,
       13-Jun-2013.) $)
    pm2.61iine $p |- ph $=
      ( wceq wn wne df-ne syl2anbr ex pm2.61ii ) BDIZCEIZAPJZQJZARBDKCEKASBDLCE
      LFMNGHO $.
      $( [16-Sep-2013] $) $( [13-Jun-2013] $)
  $}

  ${
    $d A x y z w $.  $d B x y z w $.
    $( The Dedekind cut theorem.  This theorem, which may be used to replace
       ~ ax-pre-sup with appropriate adjustments, states that, if ` A `
       completely preceeds ` B ` , then there is some number separating the two
       of them.  (Contributed by Scott Fenton, 13-Jun-2013.) $)
    dedekind $p |- ( ( A C_ RR /\ B C_ RR /\ A. x e. A A. y e. B x < y ) ->
       E. z e. RR A. x e. A A. y e. B ( x <_ z /\ z <_ y ) ) $=
      ( vw cr wss cv clt wbr wral cle wa wrex wi c0 wne w3a wn simp2l simp1l
      wcel wex simp1r n0 sylib simp2r sseld ralcom weq breq2 ralbidv rcla4cv
      sylbi 3ad2ant3 jcad eximdv mpd df-rex sylibr axsup syl111anc ax-17 hbra1
      hb3an hban hbral simprrl r19.21bi wb simpl2l ssel2 sylan simplrl lenlt
      syl11anc mpbird ex simpl3 simp2 simpr ra4 com12 adantl adantlr adantr
      simplr ltnsym syld an32s ralimdva syl2an notbid cbvralv ralnex simplrr
      breq1 rexbidv imbi12d rcla4v sylc mtod simprll expr anim12d exp3a
      r19.21ad r19.21ai reximdva 3expib wceq c1 1re ral0 raleq mpbiri anbi12d
      2ralbidv rcla4ev sylancr a1d r19.21aiv pm2.61iine 3impa ) DGHZEGHZAIZBIZJ
      KZBELZADLZYHCIZMKZYMYIMKZNZBELZADLZCGOZYFYGNZYLNZYSPDEQQDQRZEQRZNZYTYLYSU
      UDYTYLSZYMYHJKTZADLZYHYMJKZYHFIZJKZFDOZPZAGLZNZCGOZYSUUEYFUUBUUHADLZCGOZU
      UOUUDYFYGYLUAUUBUUCYTYLUBUUEYMGUCZUUPNZCUDZUUQUUEYMEUCZCUDZUUTUUEUUCUVBUU
      BUUCYTYLUECEUFUGUUEUVAUUSCUUEUVAUURUUPUUEEGYMUUDYFYGYLUHZUIYLUUDUVAUUPPZY
      TYLYJADLZBELUVDYJABDEUJUVEUUPBYMEBCUKYJUUHADYIYMYHJULUMUNUOUPUQURUSUUPCGU
      TVACAFDVBVCUUEUUNYRCGUUEUURUUNYRUUEUURUUNNZNZYQADUUEUVFAUUDYTYLAUUDAVDYTA
      VDYKADVEVFUURUUNAUURAVDUUGUUMAUUFADVEUULAGVEVGVGVGUVGYHDUCZYPBEUUEUVFBUUD
      YTYLBUUDBVDYTBVDYKBADUVHBVDZYJBEVEVHVFUVFBVDVGUVIUVGUVHYIEUCZYPUVGUVHYNUV
      JYOUVGUVHYNUVGUVHNZYNUUFUVGUUFADUUEUURUUGUUMVIVJUVKYHGUCZUURYNUUFVKUVGYFU
      VHUVLYFYGUUDYLUVFVLDGYHVMZVNUUEUURUUNUVHVOYHYMVPVQVRVSUUEUVFUVJYOUUEUVFUV
      JNZNZYOYIYMJKZTZUVOUVPYIUUIJKZFDOZUVOUVRTZFDLZUVSTUVOYIYHJKZTZADLZUWAUVOY
      LUWDUUDYTYLUVNVTUUEYTUVJYLUWDPUVNUUDYTYLWAUVFUVJWBZYTUVJNYKUWCADYTUVHUVJY
      KUWCPYTUVHNZUVJNZYKYJUWCUVJYKYJPUWFYKUVJYJYJBEWCWDWEUWGUVLYIGUCZYJUWCPUWF
      UVLUVJYFUVHUVLYGUVMWFWGUWFYGUVJUWHYFYGUVHWHEGYIVMZVNYHYIWIVQWJWKWLWMUSUWC
      UVTAFDAFUKUWBUVRYHUUIYIJULWNWOUGUVRFDWPUGUVOUWHUUMUVPUVSPZUUEYGUVJUWHUVNU
      VCUWEUWIWMZUVNUUMUUEUURUUGUUMUVJWQWEUULUWJAYIGABUKZUUHUVPUUKUVSYHYIYMJWRU
      WLUUJUVRFDYHYIUUIJWRWSWTXAXBXCUVOUURUWHYOUVQVKUUEUURUUNUVJXDUWKYMYIVPVQVR
      XEXFXGXHXIXEXJUSXKDQXLZYSUUAUWMXMGUCZYHXMMKZXMYIMKZNZBELZADLZYSXNUWMUWSUW
      RAQLUWRAXOUWRADQXPXQYRUWSCXMGYMXMXLZYPUWQABDEUWTYNUWOYOUWPYMXMYHMULYMXMYI
      MWRXRXSXTZYAYBEQXLZYSUUAUXBUWNUWSYSXNUXBUWRADUXBUWRUVHUXBUWRUWQBQLUWQBXOU
      WQBEQXPXQYBYCUXAYAYBYDYE $.
      $( [16-Sep-2013] $) $( [13-Jun-2013] $)

    $( The Dedekind cut theorem, with the hypothesis weakened to only require
       non-strict less than.  (Contributed by Scott Fenton, 2-Jul-2013.) $)
    dedekindle $p |- ( ( A C_ RR /\ B C_ RR /\ A. x e. A A. y e. B x <_ y ) ->
       E. z e. RR A. x e. A A. y e. B ( x <_ z /\ z <_ y ) ) $=
      ( vw cr wss cv cle wbr wral w3a wa wrex wi cin c0 wceq clt simpr1 simpr2
      wcel wne wn simp1 simpl disj ra4 sylbi imp syl2an weq eleq1 biimpcd
      necon3bd ad2antll mpd wb simp2 ssel2 simp3 simpr ltlen syl11anc biimprd
      mpan2d anassrs ralimdva 3exp 3imp2 dedekind syl111anc ex wex n0 inss1
      sseli ax-17 hbra1 hb3an hban hbral inss2 breq2 rcla4cv syl5 syl6 com23
      imp32 3ad2antl3 adantr breq1 ralbidv rcla4cva r19.21bi jca r19.21ai expr
      anbi12d 2ralbidv rcla4ev expcom 19.23aiv pm2.61ine ) DGHZEGHZAIZBIZJKZBEL
      ZADLZMZXHCIZJKZXNXIJKZNZBELADLZCGOZPZDEQZRYARSZXMXSYBXMNXFXGXHXITKZBELZAD
      LZXSYBXFXGXLUAYBXFXGXLUBYBXFXGXLYEYBXFXGXLYEPYBXFXGMZXKYDADYFXHDUCZNXJYCB
      EYFYGXIEUCZXJYCPYFYGYHNZNZXJXIXHUDZYCYJXHEUCZUEZYKYFYBYGYMYIYBXFXGUFYGYHU
      GZYBYGYMYBYMADLYGYMPADEUHYMADUIUJUKULYHYMYKPYFYGYHYLXIXHBAUMYHYLXIXHEUNUO
      UPUQURYJYCXJYKNZYJXHGUCZXIGUCZYCYOUSYFXFYGYPYIYBXFXGUTYNDGXHVAULYFXGYHYQY
      IYBXFXGVBYGYHVCEGXIVAULXHXIVDVEVFVGVHVIVIVJVKABCDEVLVMVNYARUDFIZYAUCZFVOX
      TFYAVPYSXTFXMYSXSXMYSNZYRGUCZXHYRJKZYRXIJKZNZBELZADLZXSXMXFYRDUCZUUAYSXFX
      GXLUFYADYRDEVQVRZDGYRVAULYTUUEADXMYSAXFXGXLAXFAVSXGAVSXKADVTWAYSAVSWBXMYS
      YGUUEXMYSYGNZNZUUDBEXMUUIBXFXGXLBXFBVSXGBVSXKBADYGBVSXJBEVTWCWAUUIBVSWBUU
      JYHUUDUUJYHNUUBUUCUUJUUBYHXLXFUUIUUBXGXLYSYGUUBXLYGYSUUBXLYGXKYSUUBPXKADU
      IYSYREUCXKUUBYAEYRDEWDVRXJUUBBYREXIYRXHJWEWFWGWHWIWJWKWLUUJUUCBEXMXLUUGUU
      CBELZUUIXFXGXLVBYSUUGYGUUHWLXKUUKAYRDAFUMXJUUCBEXHYRXIJWMWNWOULWPWQVNWRWS
      WRXRUUFCYRGCFUMZXQUUDABDEUULXOUUBXPUUCXNYRXHJWEXNYRXIJWMWTXAXBVEXCXDUJXE
      $.
      $( [16-Sep-2013] $) $( [2-Jul-2013] $)
  $}

  $( A generalized form of the cancellation law for multiplication.
     (Contributed by Scott Fenton, 17-Jun-2013.) $)
  mulcan1g $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
    ( ( A x. B ) = ( A x. C ) <-> ( A = 0 \/ B = C ) ) ) $=
    ( cc wcel w3a cmin co cmul cc0 wceq wo subdi eqeq1d wb simp1 subcl 3adant1
    mul0or syl11anc subeq0 orbi2d bitrd axmulcl 3adant3 3adant2 3bitr3rd ) ADEZ
    BDEZCDEZFZABCGHZIHZJKZABIHZACIHZGHZJKZAJKZBCKZLZUOUPKZUKUMUQJABCMNUKUNUSULJ
    KZLZVAUKUHULDEZUNVDOUHUIUJPUIUJVEUHBCQRAULSTUKVCUTUSUIUJVCUTOUHBCUARUBUCUKU
    ODEZUPDEZURVBOUHUIVFUJABUDUEUHUJVGUIACUDUFUOUPUATUG $.
    $( [16-Sep-2013] $) $( [17-Jun-2013] $)

  $( A generalized form of the cancellation law for multiplication.
     (Contributed by Scott Fenton, 17-Jun-2013.) $)
  mulcan2g $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
    ( ( A x. C ) = ( B x. C ) <-> ( A = B \/ C = 0 ) ) ) $=
    ( cc wcel w3a cmul co wceq cc0 wo mulcom 3adant2 3adant1 eqeq12d wb
    mulcan1g 3coml orcom syl6bb bitrd ) ADEZBDEZCDEZFZACGHZBCGHZICAGHZCBGHZIZAB
    IZCJIZKZUEUFUHUGUIUBUDUFUHIUCACLMUCUDUGUIIUBBCLNOUEUJULUKKZUMUDUBUCUJUNPCAB
    QRULUKSTUA $.
    $( [16-Sep-2013] $) $( [17-Jun-2013] $)

  $( Membership in the non-negative reals.  (Contributed by Scott Fenton,
     21-Jun-2013.) $)
  elrege0 $p |- ( A e. ( 0 [,) +oo ) <-> ( A e. RR /\ 0 <_ A ) ) $=
    ( cc0 cpnf cico co wcel cxr cle wbr clt w3a cr wa wb 0re rexr ax-mp pnfxr
    elico1 mp2an cmnf xrrebnd 3ad2ant1 mnflt wi mnfxr xrltletr mp3an12 mpani
    imp 3adant3 simp3 mpbir2and simp2 jca adantr simpr ltpnf 3jca impbii bitri
    ) ABCDEFZAGFZBAHIZACJIZKZALFZVDMZBGFZCGFVBVFNBLFZVIOBPQZRBCASTVFVHVFVGVDVFV
    GUAAJIZVEVCVDVGVLVEMNVEAUBUCVCVDVLVEVCVDVLVCUABJIZVDVLVJVMOBUDQUAGFVIVCVMVD
    MVLUEUFVKUABAUGUHUIUJUKVCVDVEULUMVCVDVEUNUOVHVCVDVEVGVCVDAPUPVGVDUQVGVEVDAU
    RUPUSUTVA $.
    $( [16-Sep-2013] $) $( [21-Jun-2013] $)

  $( Cancellation law for subtraction.  (Contributed by Scott Fenton,
     21-Jun-2013.) $)
  npncan2 $p |- ( ( A e. CC /\ B e. CC ) -> ( ( A - B ) + ( B - A ) ) = 0 ) $=
    ( cc wcel wa cmin co caddc cc0 wceq npncan 3anidm13 subid adantr eqtrd ) AC
    DZBCDZEABFGBAFGHGZAAFGZIPQRSJABAKLPSIJQAMNO $.
    $( [16-Sep-2013] $) $( [21-Jun-2013] $)

  $( Cancellation law for subtraction.  (Contributed by Scott Fenton,
     23-Jun-2013.) $)
  npncan3 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
    ( ( A - B ) + ( C - A ) ) = ( C - B ) ) $=
    ( cc wcel w3a cmin co caddc cneg wceq subcl 3adant3 ancoms 3adant2 addcom
    syl11anc negsubdi2 opreq2d negsub 3eqtr2d nnncan2 3com13 eqtrd ) ADEZBDEZCD
    EZFZABGHZCAGHZIHZUJBAGHZGHZCBGHZUHUKUJUIIHZUJULJZIHZUMUHUIDEZUJDEZUKUOKUEUF
    URUGABLMUEUGUSUFUGUEUSCALNOZUIUJPQUHUPUIUJIUEUFUPUIKZUGUFUEVABARNMSUHUSULDE
    ZUQUMKUTUEUFVBUGUFUEVBBALNMUJULTQUAUGUFUEUMUNKCBAUBUCUD $.
    $( [16-Sep-2013] $) $( [23-Jun-2013] $)

  $( Swap the order of subtraction in a multiplication.  (Contributed by Scott
     Fenton, 24-Jun-2013.) $)
  mulsub2 $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
    ( ( A - B ) x. ( C - D ) ) = ( ( B - A ) x. ( D - C ) ) ) $=
    ( cc wcel wa cmin co cneg cmul wceq subcl mul2neg syl2an negsubdi2
    opreqan12d eqtr3d ) AEFBEFGZCEFDEFGZGABHIZJZCDHIZJZKIZUAUCKIZBAHIZDCHIZKISU
    AEFUCEFUEUFLTABMCDMUAUCNOSTUBUGUDUHKABPCDPQR $.
    $( [16-Sep-2013] $) $( [24-Jun-2013] $)

  $( Swap the order of subtraction in a division.  (Contributed by Scott
     Fenton, 24-Jun-2013.) $)
  div2sub $p |- ( ( ( A e. CC /\ B e. CC ) /\
    ( C e. CC /\ D e. CC /\ C =/= D ) ) ->
    ( ( A - B ) / ( C - D ) ) = ( ( B - A ) / ( D - C ) ) ) $=
    ( cc wcel wa wne w3a cmin co cneg cdiv cc0 wceq subcl 3adant3 subeq0
    necon3bid biimprd 3impia jca div2neg 3expb syl2an negsubdi2 opreqan12d
    eqtr3d ) AEFBEFGZCEFZDEFZCDHZIZGABJKZLZCDJKZLZMKZUNUPMKZBAJKZDCJKZMKUIUNEFZ
    UPEFZUPNHZGURUSOZUMABPUMVCVDUJUKVCULCDPQUJUKULVDUJUKGZVDULVFUPNCDCDRSTUAUBV
    BVCVDVEUNUPUCUDUEUIUMUOUTUQVAMABUFUJUKUQVAOULCDUFQUGUH $.
    $( [16-Sep-2013] $) $( [24-Jun-2013] $)

  $( A condition for multiplication to be non-negative.  (Contributed by Scott
     Fenton, 25-Jun-2013.) $)
  mulge0b $p |- ( ( A e. RR /\ B e. RR ) ->
    ( 0 <_ ( A x. B ) <->
      ( ( A <_ 0 /\ B <_ 0 ) \/ ( 0 <_ A /\ 0 <_ B ) ) ) ) $=
    ( cr wcel wa cc0 cmul co cle wbr wo wn ianor clt wb 0re ltnle mpan adantr
    adantl orbi12d wi ltle imp ad2ant2rl cdiv axmulrcl simprl simpll simprr
    divge0 syl22anc wceq cc recn mulcom syl2an opreq1d wne ad2antlr ad2antrr
    gt0ne0 div23 syl112anc divcan1 syl111anc 3eqtrd breqtrd jca expr simplr
    ad2ant2l eqtrd jaod sylbird syl5bi orrd ex cneg renegcl mulge0 an4s
    le0neg1 bi2anan9 mul2neg breq2d bicomd 3imtr4d impbid ) ACDZBCDZEZFABGHZIJZ
    AFIJZBFIJZEZFAIJZFBIJZEZKZWLWNXAWLWNEZWQWTWQLWOLZWPLZKZXBWTWOWPMXBXEFANJZFB
    NJZKZWTWLXHXEOWNWLXFXCXGXDWJXFXCOZWKFCDZWJXIPFAQRSWKXGXDOZWJXJWKXKPFBQRTUAS
    XBXFWTXGWLWNXFWTWLWNXFEZEZWRWSWJXFWRWKWNWJXFWRXJWJXFWRUBPFAUCRUDUEXMFWMAUFH
    ZBIXMWMCDZWNWJXFFXNIJWLXOXLABUGZSWLWNXFUHWJWKXLUIWLWNXFUJWMAUKULXMXNBAGHZAU
    FHZBAUFHAGHZBXMWMXQAUFWLWMXQUMZXLWJAUNDZBUNDZXTWKAUOZBUOZABUPUQSURXMYBYAYAA
    FUSZXRXSUMWKYBWJXLYDUTZWJYAWKXLYCVAZYGWJXFYEWKWNAVBUEZBAAVCVDXMYBYAYEXSBUMY
    FYGYHBAVEVFVGVHVIVJWLWNXGWTWLWNXGEZEZWRWSYJFWMBUFHZAIYJXOWNWKXGFYKIJWLXOYIX
    PSWLWNXGUHWJWKYIVKWLWNXGUJWMBUKULYJYKABUFHBGHZAYJYAYBYBBFUSZYKYLUMWJYAWKYIY
    CVAZWKYBWJYIYDUTZYOWKXGYMWJWNBVBVLZABBVCVDYJYAYBYMYLAUMYNYOYPABVEVFVMVHWKXG
    WSWJWNWKXGWSXJWKXGWSUBPFBUCRUDVLVIVJVNVOVPVQVRWLWQWNWTWLFAVSZIJZFBVSZIJZEZF
    YQYSGHZIJZWQWNWJYQCDZYSCDZUUAUUCUBWKAVTBVTUUDUUEEUUAUUCUUDYRUUEYTUUCYQYSWAW
    BVRUQWJWOYRWKWPYTAWCBWCWDWLUUCWNWLUUBWMFIWJYAYBUUBWMUMWKYCYDABWEUQWFWGWHWLW
    TWNWJWRWKWSWNABWAWBVRVNWI $.
    $( [16-Sep-2013] $) $( [25-Jun-2013] $)


  $( A condition for multiplication to be non-positive.  (Contributed by Scott
     Fenton, 25-Jun-2013.) $)
  mulle0b $p |- ( ( A e. RR /\ B e. RR ) ->
    ( ( A x. B ) <_ 0 <->
      ( ( A <_ 0 /\ 0 <_ B ) \/ ( 0 <_ A /\ B <_ 0 ) ) ) ) $=
    ( cr wcel wa cmul co cc0 cle wbr cneg wo wb remulcl le0neg1 syl le0neg2
    anbi2d orbi12d adantl renegcl mulge0b sylan2 cc recn mulneg2 breq2d syl2an
    3bitr2rd bitrd ) ACDZBCDZEZABFGZHIJZHUNKZIJZAHIJZHBIJZEZHAIJZBHIJZEZLZUMUNC
    DUOUQMABNUNOPUMVDURBKZHIJZEZVAHVEIJZEZLZHAVEFGZIJZUQULVDVJMUKULUTVGVCVIULUS
    VFURBQRULVBVHVABORSTULUKVECDVLVJMBUAAVEUBUCUKAUDDZBUDDZVLUQMULAUEBUEVMVNEVK
    UPHIABUFUGUHUIUJ $.
    $( [16-Sep-2013] $) $( [25-Jun-2013] $)

  $( A condition for multiplication of subtraction to be non-positive.
     (Contributed by Scott Fenton, 25-Jun-2013.) $)
  mulsuble0b $p |- ( ( A e. RR /\ B e. RR /\ C e. RR ) ->
    ( ( ( A - B ) x. ( C - B ) ) <_ 0 <->
      ( ( A <_ B /\ B <_ C ) \/ ( C <_ B /\ B <_ A ) ) ) ) $=
    ( cr wcel w3a cmin co cmul cc0 cle wbr wa wo wb resubcl 3adant3 ancoms
    3adant1 mulle0b syl11anc suble0 subge0 anbi12d ancom syl6bb orbi12d bitrd
    ) ADEZBDEZCDEZFZABGHZCBGHZIHJKLZUMJKLZJUNKLZMZJUMKLZUNJKLZMZNZABKLZBCKLZMZC
    BKLZBAKLZMZNULUMDEZUNDEZUOVBOUIUJVIUKABPQUJUKVJUIUKUJVJCBPRSUMUNTUAULURVEVA
    VHULUPVCUQVDUIUJUPVCOUKABUBQUJUKUQVDOZUIUKUJVKCBUCRSUDULVAVGVFMVHULUSVGUTVF
    UIUJUSVGOUKABUCQUJUKUTVFOZUIUKUJVLCBUBRSUDVGVFUEUFUGUH $.
    $( [16-Sep-2013] $) $( [25-Jun-2013] $)

  $( An interval law for less than or equal.  (Contributed by Scott Fenton,
     27-Jun-2013.) $)
  relin01 $p |- ( A e. RR ->
    ( A <_ 0 \/ ( 0 <_ A /\ A <_ 1 ) \/ 1 <_ A ) ) $=
    ( cr wcel cc0 cle wbr c1 wa wo w3o 1re letric mpan2 0re pm3.21 orim2d
    syl5com orim1d mpd df-3or sylibr ) ABCZADEFZDAEFZAGEFZHZIZGAEFZIZUCUFUHJUBU
    EUHIZUIUBGBCUJKAGLMUBUEUGUHUBUCUDIZUEUGUBDBCUKNADLMUEUDUFUCUEUDOPQRSUCUFUHT
    UA $.
    $( [16-Sep-2013] $) $( [27-Jun-2013] $)

  $( The upper integers starting from a natural are a subset of the naturals.
     (Contributed by Scott Fenton, 29-Jun-2013.) $)
  uznnssnn $p |- ( N e. NN -> ( ZZ>= ` N ) C_ NN ) $=
    ( cn wcel cuz cfv c1 wss elnnuz uzss sylbi nnuz syl6sseqr ) ABCZADEZFDEZBMA
    OCNOGAHFAIJKL $.
    $( [16-Sep-2013] $) $( [29-Jun-2013] $)

  $( Bring a term in a subtraction into the numerator.  (Contributed by Scott
     Fenton, 3-Jul-2013.) $)
  subdivcomb1 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
     ( ( ( C x. A ) - B ) / C ) = ( A - ( B / C ) ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cmin cdiv wceq simp3l simp1 mulcl
    syl11anc divsubdir syld3an1 divcan3 3expb 3adant2 opreq1d eqtrd ) ADEZBDEZC
    DEZCFGZHZIZCAJKZBLKCMKZULCMKZBCMKZLKZAUOLKULDEZUGUFUJUMUPNUKUHUFUQUFUGUHUIO
    UFUGUJPCAQRULBCSTUKUNAUOLUFUJUNANZUGUFUHUIURACUAUBUCUDUE $.
    $( [16-Sep-2013] $) $( [3-Jul-2013] $)

  $( Bring a term in a subtraction into the numerator.  (Contributed by Scott
     Fenton, 3-Jul-2013.) $)
  subdivcomb2 $p |- ( ( A e. CC /\ B e. CC /\ ( C e. CC /\ C =/= 0 ) ) ->
     ( ( A - ( C x. B ) ) / C ) = ( ( A / C ) - B ) ) $=
    ( cc wcel cc0 wne wa w3a cmul co cmin cdiv wceq simp3l simp2 mulcl
    syl11anc divsubdir syld3an2 simprl simpl simpr div23 syl111anc c1 divid
    opreq1d mulid2 sylan9eqr eqtrd 3adant1 opreq2d ) ADEZBDEZCDEZCFGZHZIZACBJKZ
    LKCMKZACMKZUTCMKZLKZVBBLKUNUTDEZUOURVAVDNUSUPUOVEUNUOUPUQOUNUOURPCBQRAUTCST
    USVCBVBLUOURVCBNUNUOURHZVCCCMKZBJKZBVFUPUOURVCVHNUOUPUQUAUOURUBUOURUCCBCUDU
    EURUOVHUFBJKBURVGUFBJCUGUHBUIUJUKULUMUK $.
    $( [16-Sep-2013] $) $( [3-Jul-2013] $)

  $( Relationship between subtraction and addition.  (Contributed by Scott
     Fenton, 5-Jul-2013.) $)
  subadd2 $p |- ( ( A e. CC /\ B e. CC /\ C e. CC ) ->
            ( ( A - B ) = C <-> ( C + B ) = A ) ) $=
    ( cc wcel cmin co wceq caddc wb c1 cif opreq1 eqeq1d eqeq2 bibi12d opreq2
    ax-1cn elimel subadd2i dedth3h ) ADEZBDEZCDEZABFGZCHZCBIGZAHZJUBAKLZBFGZCHZ
    UGUIHZJUIUCBKLZFGZCHZCUMIGZUIHZJUNUDCKLZHZURUMIGZUIHZJABCKKKAUIHZUFUKUHULVB
    UEUJCAUIBFMNAUIUGOPBUMHZUKUOULUQVCUJUNCBUMUIFQNVCUGUPUIBUMCIQNPCURHZUOUSUQV
    ACURUNOVDUPUTUICURUMIMNPUIUMURAKDRSBKDRSCKDRSTUA $.
    $( [16-Sep-2013] $) $( [5-Jul-2013] $)

  $( Reverse the order of subtraction in an equality.  (Contributed by Scott
     Fenton, 8-Jul-2013.) $)
  subeqrev $p |- ( ( ( A e. CC /\ B e. CC ) /\ ( C e. CC /\ D e. CC ) ) ->
    ( ( A - B ) = ( C - D ) <-> ( B - A ) = ( D - C ) ) ) $=
    ( cc wcel wa cmin co cneg wceq wb subcl neg11 syl2an negsubdi2 eqeqan12d
    bitr3d ) AEFBEFGZCEFDEFGZGABHIZJZCDHIZJZKZUAUCKZBAHIZDCHIZKSUAEFUCEFUEUFLTA
    BMCDMUAUCNOSTUBUGUDUHABPCDPQR $.
    $( [16-Sep-2013] $) $( [8-Jul-2013] $)

  $( Shift membership in a finite sequence of naturals.  (Contributed by Scott
     Fenton, 17-Jul-2013.) $)
  fznatpl1 $p |- ( ( N e. NN /\ I e. ( 1 ... ( N - 1 ) ) ) ->
           ( I + 1 ) e. ( 1 ... N ) ) $=
    ( cn wcel c1 cmin co cfz wa caddc cle wbr cz wb elfzelz peano2zdi adantl
    nnz adantr 1z elfz mp3an2 syl11anc cr clt zre syl peano2re 1re a1i ltp1
    elfzle1 leadd1 mp3an13 mpbid ltletrd wi ltle mpan sylc elfzle2 leaddsub
    mpbird mpbir2and ) BCDZAEBEFGZHGDZIZAEJGZEBHGDZEVIKLZVIBKLZVHVIMDZBMDZVJVKV
    LINZVGVMVEVGAAEVFOZPQVEVNVGBRSZVMEMDVNVOTVIEBUAUBUCVHVIUDDZEVIUELZVKVHAUDDZ
    VRVGVTVEVGAMDVTVPAUFUGQZAUHUGZVHEEEJGZVIEUDDZVHUIUJZVHWDWCUDDWEEUHUGWBVHWDE
    WCUELWEEUKUGVHEAKLZWCVIKLZVGWFVEAEVFULQVHVTWFWGNZWAWDVTWDWHUIUIEAEUMUNUGUOU
    PWDVRVSVKUQUIEVIURUSUTVHVLAVFKLZVGWIVEAEVFVAQVHVTBUDDZVLWINZWAVHVNWJVQBUFUG
    VTWDWJWKUIAEBVBUBUCVCVD $.
    $( [16-Sep-2013] $) $( [17-Jul-2013] $)

  ${
    $d x y m n z $.
    $( Establish the domain and codomain of the finite integer sequence
       function.  (Contributed by Scott Fenton, 8-Aug-2013.) $)
    fzf $p |- ... : ( ZZ X. ZZ ) --> ~P ZZ $=
      ( vx vy vm vn vz cz cxp cpw cfz wf wfn cv co wcel wral ffnoprv cle wbr
      wa crab zex rabex df-fz fnoprab2 wss cuz cfv fzssuz uzssz sstri oprex
      elpw mpbir a1i rgen2a mpbir2an ) FFGZFHZIJIUQKALZBLZIMZURNZBFOAFOABFFURIP
      CDEFFCLUTQRUTDLQRSZBFTIVCBFUAUBEBCDUCUDVBABFVBUSFNUTFNSVBVAFUEVAUSUFUGFUS
      UTUHUSUIUJVAFUSUTIUKULUMUNUOUP $.
      $( [16-Sep-2013] $) $( [8-Aug-2013] $)

    $( Establish the domain and codomain of the upper integer function.
       (Contributed by Scott Fenton, 8-Aug-2013.) $)
    uzf $p |- ZZ>= : ZZ --> ~P ZZ $=
      ( vm vn vx cz cpw cv cle wbr crab cuz df-uz wcel wss ssrab2 zex elpw2
      mpbir a1i fopab ) ABDDEZAFZCFGHZCDIZJBACKUCTLZUADLUDUCDMUBCDNUCDOPQRS $.
      $( [16-Sep-2013] $) $( [8-Aug-2013] $)
  $}


  ${
    $d N x $.
    ltweuzlem1.1 $e |- N e. ZZ $.
    $( Lemma for ~ ltweuz .  Less than well orders a set of upper integers
       given that the argument to ` ZZ>= ` is an integer. $)
    ltweuzlem1 $p |- < We ( ZZ>= ` N ) $=
      ( ltfrn ) AC $.
      $( [16-Sep-2013] $) $( [6-Aug-2013] $)
  $}

  $( Less than well orders a set of upper integers.  (Contributed by Scott
     Fenton, 8-Aug-2013.) $)
  ltweuz $p |- < We ( ZZ>= ` N ) $=
    ( cz wcel cuz cfv clt wwe c1 cif wceq wb fveq2 weeq2 syl 1z elimel
    ltweuzlem1 dedth wn c0 we0 cdm cpw uzf fdmi eleq2i notbii ndmfv sylbir
    mpbiri pm2.61i ) ABCZADEZFGZULUNULAHIZDEZFGZAHAUOJUMUPJUNUQKAUODLUMUPFMNUOA
    HBOPQRULSZUNTFGZFUAURUMTJZUNUSKURADUBZCZSUTVBULVABABBUCDUDUEUFUGADUHUIUMTFM
    NUJUK $.
    $( [16-Sep-2013] $) $( [8-Aug-2013] $)

  $( Less than well orders the naturals.  (Contributed by Scott Fenton,
     6-Aug-2013.) $)
  ltwenn $p |- < We NN $=
    ( cn clt wwe c1 cuz cfv ltweuz wceq wb nnuz weeq2 ax-mp mpbir ) ABCZDEFZBCZ
    DGAOHNPIJAOBKLM $.
    $( [16-Sep-2013] $) $( [6-Aug-2013] $)

  $( Less than well orders a set of finite integers.  (Contributed by Scott
     Fenton, 8-Aug-2013.) $)
  ltwefz $p |- < We ( M ... N ) $=
    ( cfz co cuz cfv wss clt wwe fzssuz ltweuz wess mp2 ) ABCDZAEFZGOHINHIABJAK
    NOHLM $.
    $( [16-Sep-2013] $) $( [8-Aug-2013] $)

  ${
    $d N x $.  $d M x $.
    $( The supremum of a finite sequence of integers.  (Contributed by Scott
       Fenton, 8-Aug-2013.) $)
    supfz $p |- ( N e. ( ZZ>= ` M ) -> sup ( ( M ... N ) , ZZ , < ) = N ) $=
      ( vx cuz cfv wcel cz cfz co cv clt wbr wn wral csup wceq eluzelz eluzfz2
      wa cle elfzle2 adantl wb cr elfzelz zre syl lenlt syl2an ancoms mpbid
      r19.21aiva wss wor zssre ltso soss mp2 supmax syl111anc ) BADEFZBGFZBABHI
      ZFBCJZKLMZCVCNVCGKOBPABQZABRVAVECVCVAVDVCFZSVDBTLZVEVGVHVAVDABUAUBVGVAVHV
      EUCZVGVDUDFZBUDFZVIVAVGVDGFVJVDABUEVDUFUGVAVBVKVFBUFUGVDBUHUIUJUKULCGVCBK
      GUDUMUDKUNGKUNUOUPGUDKUQURUSUT $.
      $( [16-Sep-2013] $) $( [8-Aug-2013] $)

    $( The infimum of a finite sequence of integers.  (Contributed by Scott
       Fenton, 8-Aug-2013.) $)
    inffz $p |- ( N e. ( ZZ>= ` M ) -> sup ( ( M ... N ) , ZZ , `' < ) = M ) $=
      ( vx cuz cfv wcel cz cfz co cv clt ccnv wbr wn wral csup wceq eluzel2
      eluzfz1 wa cle elfzle1 adantl cr wb zre syl elfzelz lenlt syl2an mpbid
      brcnvg notbid sylan mpbird r19.21aiva wor wss zssre ltso soss mp2 cnvso
      mpbi supmax syl111anc ) BADEFZAGFZAABHIZFACJZKLZMZNZCVIOVIGVKPAQABRZABSVG
      VMCVIVGVJVIFZTZVMVJAKMZNZVPAVJUAMZVRVOVSVGVJABUBUCVGAUDFZVJUDFZVSVRUEVOVG
      VHVTVNAUFUGVOVJGFWAVJABUHVJUFUGAVJUIUJUKVGVHVOVMVRUEVNVHVOTVLVQAVJGVIKULU
      MUNUOUPCGVIAVKGKUQZGVKUQGUDURUDKUQWBUSUTGUDKVAVBGKVCVDVEVF $.
      $( [16-Sep-2013] $) $( [8-Aug-2013] $)

  $}

