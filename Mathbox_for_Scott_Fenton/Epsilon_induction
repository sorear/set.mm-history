$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Epsilon induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d ph y z w $.  $d x y z w $.
    setinds.1 $e |- ( A. y e. x [ y / x ] ph -> ph ) $.
    $( Principle of ` _E ` induction (set induction).  If a property passes
       from all elements of ` x ` to ` x ` itself, then it holds for all
       ` x ` .  (Contributed by Scott Fenton, 10-Mar-2011.) $)
    setinds $p |- ph $=
      ( vz vw cv cvv wcel visset cab wss wceq wral wsb df-clab wel ax-17 hbs1
      wi setind ralbii hbral hbim weq sbequ12 imbi12d chvar sylbi dfss3 3imtr4i
      raleqf mpg eqcomi abeq2i mpbi ) BGZHIABJABHABKZHEGZURLZUSURIZTURHMEEURUAC
      GURIZCUSNZABEOZUTVAVCABCOZCUSNZVDVBVECUSACBPUBVECUQNZATVFVDTBEVFVDBVEBCUS
      CEQBRABCSUCABESUDBEUEVGVFAVDVECFUQUSFBQCRFEQCRULABEUFUGDUHUICUSURUJAEBPUK
      UMUNUOUP $.
      $( [10-Mar-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.
    setinds2f.1 $e |- ( ps -> A. x ps ) $.
    setinds2f.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    setinds2f.3 $e |- ( A. y e. x ps -> ph ) $.
    $( ` _E ` induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 10-Mar-2011.) $)
    setinds2f $p |- ph $=
      ( wsb cv wral sbie ralbii sylbi setinds ) ACDACDHZDCIZJBDPJAOBDPABCDEFKLG
      MN $.
      $( [10-Mar-2011] $)
  $}

  ${
    $d x y $.  $d ph y $.  $d ps x $.
    setinds2.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    setinds2.2 $e |- ( A. y e. x ps -> ph ) $.
    $( ` _E ` induction schema, using implicit substitution.  (Contributed by
       Scott Fenton, 10-Mar-2011.) $)
    setinds2 $p |- ph $=
      ( ax-17 setinds2f ) ABCDBCGEFH $.
      $( [10-Mar-2011] $)
  $}

