$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Sum of reciprocals of triangle numbers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d F j $.  $d F k $.  $d F x $.  $d j k $.  $d j x $.  $d k x $.  $d N k $.
    $d n k $.  $d N n $.  $d j n $.
    trireciplem.1 $e |- F = ( n e. NN |-> ( 1 / ( n x. ( n + 1 ) ) ) ) $.
    $( Lemma for ~ trirecip .  Show that the sum converges.  (Contributed by
       Scott Fenton, 22-Apr-2014.)  (Revised by Mario Carneiro,
       22-May-2014.) $)
    trireciplem $p |- seq 1 ( + , F ) ~~> 1 $=
      ( vj caddc c1 cc0 cmin co wtru cn cdiv cvv wcel a1i ax-1cn syl wceq cmul
      cc vk cseq cli wbr cv cmpt nnuz cz 1z 0cn divcnv nnex mptex wa cfv adantl
      peano2nn opreq2 eqid oprex fvmpt opreq1 opreq2d eqtr4d climshft2 seqex cr
      mpbird nnrecre recnd eqeltrd cfz csu elfznn nncn peano2cn nnmulcl syl2anc
      wne nnne0 divsubdir pncan2 sylancl opreq1d mulid1 mulcom opreq12d divcan5
      syl112anc syl122anc eqtr3d 3eqtr3d sumeq2dv syl6eq cuz syl6eleq fsumtscop
      div1i simpr eqtrd id fsumser 3eqtr2rd climsubc2 trud subid1i breqtri ) EB
      FUBZFGHIZFUCXHXIUCUDJGFUAAKFAUEZFEIZLIZUFZXHFTMKUGFUHNJUIOZGTNJUJOZJXMGUC
      UDAKFXJLIZUFZGUCUDZJFTNZXRXSJPOZFAUKQJGUAXMXQFFTMMKUGXNXNXOXMMNJAKXLULUMO
      XQMNJAKXPULUMOJUAUEZKNZUNZYAFEIZXQUOZFYDLIZYAXMUOZYCYDKNZYEYFRYBYHJYAUQUP
      ZAYDXPYFKXQXJYDFLURZXQUSFYDLUTZVAQYBYGYFRJAYAXLYFKXMXJYARXKYDFLXJYAFEVBVC
      XMUSYKVAUPZVDVEVHXTXHMNJEBFVFOYCYGYFTYLYCYFYCYHYFVGNYIYDVIQVJVKYCFYGHIFYF
      HIZFYAVLIZFDUEZYOFEIZSIZLIZDVMZYAXHUOYCYGYFFHYLVCYCYSYNFYOLIZFYPLIZHIZDVM
      YMYCYNYRUUBDYCYOYNNZUNZYPYOHIZYQLIZYPYQLIZYOYQLIZHIZYRUUBUUDYPTNZYOTNZYQT
      NZYQGVSZUUFUUIRUUDUUKUUJUUDYOKNZUUKUUCUUNYCYOYAVNUPZYOVOQZYOVPQZUUPUUDYQK
      NZUULUUDUUNYPKNZUURUUOUUDUUNUUSUUOYOUQQZYOYPVQVRZYQVOQUUDUURUUMUVAYQVTQYP
      YOYQWAWIUUDUUEFYQLUUDUUKXSUUEFRUUPPYOFWBWCWDUUDUUGYTUUHUUAHUUDYPFSIZYPYOS
      IZLIZUUGYTUUDUVBYPUVCYQLUUDUUJUVBYPRUUQYPWEQUUDUUJUUKUVCYQRUUQUUPYPYOWFVR
      WGUUDXSUUKYOGVSZUUJYPGVSZUVDYTRXSUUDPOZUUPUUDUUNUVEUUOYOVTQZUUQUUDUUSUVFU
      UTYPVTQZFYOYPWHWJWKUUDYOFSIZYQLIZUUHUUAUUDUVJYOYQLUUDUUKUVJYORUUPYOWEQWDU
      UDXSUUJUVFUUKUVEUVKUUARUVGUUQUVIUUPUVHFYPYOWHWJWKWGWLWMYCXPYTUUAFDAYFFYAX
      JYOFLURXJYPFLURXJFRXPFFLIFXJFFLURFPWRWNYJYCYAKFWOUOJYBWSUGWPZYCXJFYDVLINZ
      UNZXPUVNXJKNZXPVGNUVMUVOYCXJYDVNUPXJVIQVJWQWTYCYRDBFYAUUDUUNYOBUOYRRUUOAY
      OFXJXKSIZLIYRKBXJYORZUVPYQFLUVQXJYOXKYPSUVQXAXJYOFEVBWGVCCFYQLUTVAQUVLUUD
      YRUUDUURYRVGNUVAYQVIQVJXBXCXDXEFPXFXG $.
      $( [22-Apr-2014] $)
  $}

  ${
    $d k j n $.
    $( The sum of the reciprocals of the triangle numbers converge to two.
       (Contributed by Scott Fenton, 23-Apr-2014.)  (Revised by Mario Carneiro,
       22-May-2014.) $)
    trirecip $p |- sum_ k e. NN ( 2 / ( k x. ( k + 1 ) ) ) = 2 $=
      ( vn cn c2 cv c1 caddc co cmul cdiv csu wcel cc wceq 2cn a1i syl wtru cli
      nnuz cc0 peano2nn nnmulcl mpdan nncn nnne0 divrec syl3anc sumeq2i cmpt cz
      wne 1z cfv id opreq1 opreq12d opreq2d oprex fvmpt adantl wa wi cr nnrecre
      eqid recnd idi wbr cdm trireciplem seqex breldm isummulc2 isumclim eqtr3d
      cseq trud mulid1i 3eqtri ) CDAEZWAFGHZIHZJHZAKCDFWCJHZIHZAKZDFIHZDCWDWFAW
      ACLZDMLZWCMLZWCUAULZWDWFNWJWIOPWIWCCLZWKWIWBCLWMWAUBWAWBUCUDZWCUEQWIWMWLW
      NWCUFQDWCUGUHUIWGWHNRDCWEAKZIHWGWHRWEDABCFBEZWPFGHZIHZJHZUJZFCTFUKLRUMPZW
      IWAWTUNWENRBWAWSWECWTWPWANZWRWCFJXBWPWAWQWBIXBUOWPWAFGUPUQURWTVFZFWCJUSUT
      VAZRWIVBWEMLZVCWIXERWIWEWIWMWEVDLWNWCVEQVGVAZVHRGWTFVQZFSVIZXGSVJLXHRBWTX
      CVKPZXGFSGWTFVLVMQWJROPVNRWOFDIRWEFAWTFUKCTXAXDXFXAXIVOURVPVRDOVSVT $.
      $( [22-May-2014] $) $( [23-Apr-2014] $)
  $}

