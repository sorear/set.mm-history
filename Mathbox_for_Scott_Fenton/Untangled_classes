$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Untangled classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( We call a class "untanged" if all its members are not members of
       themselves.  The term originates from Isbell (see citation in
       ~ dfon2 ).  Using this concept, we can avoid a lot of the uses of the
       Axiom of Regularity.  Here, we prove a series of properties of untanged
       classes.  First, we prove that an untangled class is not a member of
       itself. $)
    untelirr $p |- ( A. x e. A -. x e. x -> -. A e. A ) $=
      ( wel wn wral wcel cv wceq eleq1 eleq2 bitrd notbid rcla4cv pm2.01d ) AAC
      ZDZABEBBFZPQDABBAGZBHZOQSOBRFQRBRIRBBJKLMN $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d x y A $.
    $( The union of a class is untangled iff all its members are untangled. $)
    untuni $p |- ( A. x e. U. A -. x e. x <-> A. y e. A A. x e. y -. x e. x )
        $=
      ( cv cuni wcel wel wn wi wal wral wrex r19.23v albii ralcom4 eluni2
      imbi1i 3bitr4ri df-ral ralbii 3bitr4i ) ADZCEZFZAAGHZIZAJZABGZUEIZAJZBCKZ
      UEAUCKUEABDZKZBCKUIBCKZAJUHBCLZUEIZAJUKUGUNUPAUHUEBCMNUIBACOUFUPAUDUOUEBU
      BCPQNRUEAUCSUMUJBCUEAULSTUA $.
      $( [28-Feb-2011] $)
  $}

  ${
    $d x A $.  $d x y $.
    untsucf.1 $e |- ( x e. A -> A. y x e. A ) $.
    $( If a class is untangled, then so is its successor. $)
    untsucf $p |- ( A. x e. A -. x e. x -> A. y e. suc A -. y e. y ) $=
      ( wel wn wral csuc ax-17 hbral cv wcel wceq wo weq elequ1 elequ2 bitrd
      notbid rcla4cv eleq1 eleq2 untelirr syl5cbir jaod visset elsuc syl5ib
      r19.21ai ) AAEZFZACGZBBEZFZBCHZUKBACDUKBIJULBKZCLZUPCMZNUNUPUOLULUQUNURUK
      UNAUPCABOZUJUMUSUJBAEUMABAPABBQRSTURUNCCLZFULURUMUTURUMCUPLUTUPCUPUAUPCCU
      BRSACUCUDUEUPCBUFUGUHUI $.
      $( [28-Feb-2011] $)
  $}

  $( The null set is untangled.  (The proof was shortened by Andrew Salmon,
     27-Aug-2011.) $)
  unt0 $p |- A. x e. (/) -. x e. x $=
    ( wel wn ral0 ) AABCAD $.
    $( [27-Aug-2011] $) $( [10-Mar-2011] $)

  ${
    $d x y A $.
    $( If there is an untangled element of a class, then the intersection of
       the class is untangled. $)
    untint $p |- ( E. x e. A A. y e. x -. y e. y ->
                    A. y e. |^| A -. y e. y ) $=
      ( wel wn cv wral cint wcel wss wi intss1 ssralv syl r19.23aiv ) BBDEZBAFZ
      GZPBCHZGZACQCISQJRTKQCLPBSQMNO $.
      $( [1-Mar-2011] $)
  $}

  ${
    $d x A $.
    $( If ` A ` is well-founded by ` _E ` , then it is untangled. $)
    efrunt $p |- ( _E Fr A -> A. x e. A -. x e. x ) $=
      ( cep wfr wel wn cv wcel wa wbr frirr epel notbii sylib r19.21aiva ) BCDZ
      AAEZFZABPAGZBHISSCJZFRABCKTQAALMNO $.
      $( [1-Mar-2011] $)
  $}

  ${
    $d x y A $.
    $( A transitive class is untangled iff its elements are. $)
    untangtr $p |- ( Tr A ->
                   ( A. x e. A -. x e. x <->
                     A. x e. A A. y e. x -. y e. y ) ) $=
      ( wtr wel wn wral cv cuni wss wi df-tr ssralv sylbi weq elequ1 elequ2
      bitrd notbid cbvralv untuni bitri syl6ib untelirr ralimi impbid1 ) CDZAAE
      ZFZACGZBBEZFZBAHZGZACGZUGUJUIACIZGZUOUGUPCJUJUQKCLUIAUPCMNUQULBUPGUOUIULA
      BUPABOZUHUKURUHBAEUKABAPABBQRSTBACUAUBUCUNUIACBUMUDUEUF $.
      $( [7-Mar-2011] $)
  $}

