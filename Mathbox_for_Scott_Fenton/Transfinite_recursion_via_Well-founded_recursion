$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Transfinite recursion via Well-founded recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y $.
    $( Lemma for deriving transfinite recursion from well-founded recursion. $)
    tfrALTlem $p |- { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } =
                    { f |
                           E. x ( f Fn x /\
                                  ( x C_ On /\
                                    A. y e. x Pred ( _E , On , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                          ( G ` ( f |` Pred ( _E , On , y ) ) )
                                ) } $=
      ( cv wfn cfv cres wceq wral con0 wss cep wex wcel syl pm5.32i wwe 3bitr4i
      wa wrex cpred w3a df-rex wel onelon predon reseq2d fveq2d eqeq2d pm5.74da
      wb ralbidv2 word wtr df-ord ordsson sylbir epweon wess mpi impbid1 visset
      ancom elon ssel2 sseq1d ralbidva syl6bbr anbi1i bitr3i anbi2i an12 3anass
      ex dftr3 exbii bitri abbii ) CEZAEZFZBEZVTGZVTWCHZDGZIZBWAJZTZAKUAZWBWAKL
      ZKMWCUBZWALZBWAJZTZWDVTWLHZDGZIZBWAJZUCZANZCWJWAKOZWITZANXAWIAKUDXCWTAWBX
      BWHTZTWBWOWSTZTXCWTXDXEWBXDXBWSTXEXBWSWHXBWRWGBWAWAXBBAUEZWRWGXBXFTWCKOZW
      RWGULWAWCUFXGWQWFWDXGWPWEDXGWLWCVTWCUGZUHUIUJPUKUMQXBWOWSWAUNZWKWAUOZTZXB
      WOXJWAMRZTZXJWKTXIXKXJXLWKXJXLWKXJXLWKXMXIWKWAUPZWAUQURVOWKKMRXLUSWAKMUTV
      AVBQXNWKXJVDSWAAVCVEWKWNXJWKWNWCWALZBWAJXJWKWMXOBWAWKXFTZWLWCWAXPXGWLWCIW
      AKWCVFXHPVGVHBWAVPVIQSVJVKVLXBWBWHVMWBWOWSVNSVQVRVS $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d B w $.  $d F f w x y $.  $d G f w x y $.  $d y z $.
    tfrALT.1 $e |- A = { f | E. x e. On ( f Fn x /\
                A. y e. x ( f ` y ) = ( G ` ( f |` y ) ) ) } $.

    $( Let ` F ` be the union of all acceptable functions. $)
    tfrALT.2 $e |- F = U. A $.
    $( ~ tfr1 via well-founded recursion.  (Contributed by Scott Fenton,
       17-Aug-1994.) $)
    tfr1ALT $p |- F Fn On $=
      ( con0 cep epweon epsetlike cv cfv cres wceq wral wa cab wss wfn wrex w3a
      cpred wex tfrALTlem eqtri wfr1 ) ABICJDEFKAILCDMZAMZUAZBMZUINZUIULOFNPBUJ
      QRAIUBDSUKUJITIJULUDZUJTBUJQRUMUIUNOFNPBUJQUCAUEDSGABDFUFUGHUH $.
      $( [5-May-2004] $) $( [17-Aug-1994] $)

    $( ~ tfr2 via well-founded recursion.  (Contributed by Scott Fenton,
       22-Apr-2011.) $)
    tfr2ALT $p |- ( z e. On -> ( F ` z ) = ( G ` ( F |` z ) ) ) $=
      ( cv con0 cfv cep cpred cres wceq wral wa cab wss wcel epsetlike wfn wrex
      epweon w3a wex tfrALTlem eqtri wfr2 predon reseq2d fveq2d eqtrd ) CJZKUAZ
      UOFLFKMUONZOZGLFUOOZGLABCKDMEFGUEAKUBDEJZAJZUCZBJZUTLZUTVCOGLPBVAQRAKUDES
      VBVAKTKMVCNZVATBVAQRVDUTVEOGLPBVAQUFAUGESHABEGUHUIIUJUPURUSGUPUQUOFUOUKUL
      UMUN $.
      $( [22-Apr-2011] $)

    ${
      $d B x $.
      $( ~ tfr3 via well-founded recursion.  (Contributed by Scott Fenton,
         22-Apr-2011.) $)
      tfr3ALT $p |- ( ( B Fn On /\
               A. x e. On ( B ` x ) = ( G ` ( B |` x ) ) ) -> B = F ) $=
        ( vw cv cfv cres wceq con0 wral wfn cep cpred wa predeq3 reseq2d fveq2d
        fveq2 eqeq12d cbvralv wcel predon eqeq2d ralbiia bitri epweon epsetlike
        weq wrex cab wss w3a wex tfrALTlem eqtri wfr3 eqcomd sylan2br ) AKZDLZD
        VEMZGLZNZAOPZDOQZJKZDLZDORVLSZMZGLZNZJOPZDFNVRVFDORVESZMZGLZNZAOPVJVQWB
        JAOJAUNZVMVFVPWAVLVEDUDWCVOVTGWCVNVSDORVLVEUAUBUCUEUFWBVIAOVEOUGZWAVHVF
        WDVTVGGWDVSVEDVEUHUBUCUIUJUKVKVRTFDABJOCREFGDULAOUMCEKZVEQZBKZWELZWEWGM
        GLNBVEPTAOUOEUPWFVEOUQORWGSZVEUQBVEPTWHWEWIMGLNBVEPURAUSEUPHABEGUTVAIVB
        VCVD $.
        $( [22-Apr-2011] $)
    $}
  $}

