$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Well-founded recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A w x y z $.  $d F w x y z $.  $d G w x y z $.  $d H w x y z $.
    $d R w x y z $.
    $( Functions defined by well founded recursion are identical up to
       relation, domain, and characteristic function.  (Contributed by Scott
       Fenton, 11-Feb-2011.) $)
    wfr3g $p |- ( ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                        ( F Fn A /\
                          A. y e. A ( F ` y ) =
                                    ( H ` ( F |` Pred ( R , A , y ) ) ) ) /\
                     ( G Fn A /\
                       A. y e. A ( G ` y ) =
                                 ( H ` ( G |` Pred ( R , A , y ) ) ) ) )
                   -> F = G ) $=
      ( vz vw cv cpred wral wa cfv cres wceq wi fveq2 eqeq12d fveq2d wwe r19.26
      cvv wcel wfn w3a weq imbi2d ax-17 ra5 predeq3 reseq2d anbi12d rcla4va wss
      wb predss fvreseq mpan2 biimpar eqcomd eqtr3 ancoms syl com12 exp3a com23
      ex expimpd imp3a syl5 wfis2g r19.21 sylib sylan2br an4s 3impib eqid jctil
      a2d eqfnfv ad2ant2r 3adant1 mpbird ) CDUACDAJKUCUDACLMZECUEZBJZENZECDWGKZ
      OZGNZPZBCLZMZFCUEZWGFNZFWIOZGNZPZBCLZMZUFZEFPZCCPZHJZENZXEFNZPZHCLZMZXBXI
      XDWEWNXAXIWNXAMWEXIWFWOWMWTWEXIQZWMWTMWFWOMZWLWSMZBCLZXKWLWSBCUBWEXLXNMZX
      IWEXOXHQZHCLXOXIQXPXOIJZENZXQFNZPZQZAHICDHIUGZXHXTXOYBXFXRXGXSXEXQERXEXQF
      RSUHYAICDXEKZLXOXTIYCLZQXECUDZXPXOXTIYCXOIUIUJYEXOYDXHYEXLXNYDXHQZYEXNXLY
      FYEXNXLYFQZYEXNMXFEYCOZGNZPZXGFYCOZGNZPZMZYGXMYNBXECBHUGZWLYJWSYMYOWHXFWK
      YIWGXEERYOWJYHGYOWIYCECDWGXEUKZULTSYOWPXGWRYLWGXEFRYOWQYKGYOWIYCFYPULTSUM
      UNYNXLYDXHXLYDMZYNXHYQYLYIPZYNXHQYQYKYHGYQYHYKXLYHYKPZYDXLYCCUOYSYDUPCDXE
      UQICYCEFURUSUTVATYRYJYMXHYRYJMXFYLPZYMXHQYJYRYTXFYLYIVBVCYTYMXHXFXGYLVBVH
      VDVIVDVEVFVDVHVGVJVTVKVLXOXHHCXOHUIVMVNVEVOVPVEVQCVRVSWNXAXCXJUPZWEWFWOUU
      AWMWTHCCEFWAWBWCWD $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A f g w x y z $.  $d G f g w x y z $.  $d R f g w x y z $.
    wfrlem1.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  The final item we are interested in
       is the union of acceptable functions ` B ` .  This lemma just changes
       bound variables for later use. $)
    wfrlem1 $p |- B = { g |
                         E. z ( g Fn z /\
                                  ( z C_ A /\
                                    A. w e. z Pred ( R , A , w ) C_ z ) /\
                                  A. w e. z ( g ` w ) =
                                            ( G ` ( g |` Pred ( R , A , w ) ) )
                                ) } $=
      ( cv wfn wss wral cfv cres wceq w3a wex cpred cab weq fneq1 reseq1 fveq2d
      fveq1 eqeq12d ralbidv 3anbi13d exbidv fneq2 sseq1 sseq2 raleqbi1dv sseq1d
      predeq3 cbvralv syl6bb anbi12d raleq fveq2 reseq2d 3anbi123d cbvexv eqtri
      wa cbvabv ) FHLZALZMZVJENZEGBLZUAZVJNZBVJOZVGZVMVIPZVIVNQZJPZRZBVJOZSZATZ
      HUBILZCLZMZWFENZEGDLZUAZWFNZDWFOZVGZWIWEPZWEWJQZJPZRZDWFOZSZCTZIUBKWDWTHI
      HIUCZWDWEVJMZVQVMWEPZWEVNQZJPZRZBVJOZSZATWTXAWCXHAXAVKXBWBXGVQVJVIWEUDXAW
      AXFBVJXAVRXCVTXEVMVIWEUGXAVSXDJVIWEVNUEUFUHUIUJUKXHWSACACUCZXBWGVQWMXGWRV
      JWFWEULXIVLWHVPWLVJWFEUMXIVPVNWFNZBWFOWLVOXJBVJWFVJWFVNUNUOXJWKBDWFBDUCZV
      NWJWFEGVMWIUQZUPURUSUTXIXGXFBWFOWRXFBVJWFVAXFWQBDWFXKXCWNXEWPVMWIWEVBXKXD
      WOJXKVNWJWEXLVCUFUHURUSVDVEUSVHVF $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  An acceptable function is a
       function. $)
    wfrlem2 $p |- ( g e. B -> Fun g ) $=
      ( vz vw cv wcel wfn wss cpred wral wa cfv cres wceq w3a wex wfrlem1 fnfun
      wfun abeq2i 3ad2ant1 exlimiv sylbi ) GLZDMUKJLZNZULCOCEKLZPZULOKULQRZUNUK
      SUKUOTHSUAKULQZUBZJUCZUKUFZUSGDABJKCDEFGHIUDUGURUTJUMUPUTUQULUKUEUHUIUJ
      $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  An acceptable function's domain is a
       subset of ` A ` . $)
    wfrlem3 $p |- ( g e. B -> dom g C_ A ) $=
      ( vz vw cv wcel wfn wss cpred wral wa cfv cres w3a wex cdm wfrlem1 abeq2i
      wceq fndm sseq1d biimpar adantrr 3adant3 exlimiv sylbi ) GLZDMUNJLZNZUOCO
      ZCEKLZPZUOOKUOQZRZURUNSUNUSTHSUFKUOQZUAZJUBZUNUCZCOZVDGDABJKCDEFGHIUDUEVC
      VFJUPVAVFVBUPUQVFUTUPVFUQUPVEUOCUOUNUGUHUIUJUKULUM $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A a b c f g h x y $.  $d B a $.  $d G a b c f g h x y $.
    $d R a b c f g h x y $.
    wfrlem4.1 $e |- R We A $.
    wfrlem4.2 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  Properties of the restriction of an
       acceptable function to the domain of another one. $)
    wfrlem4 $p |- ( ( g e. B /\ h e. B ) ->
                     ( ( g |` ( dom g i^i dom h ) ) Fn ( dom g i^i dom h ) /\
                       A. a e. ( dom g i^i dom h )
                        ( ( g |` ( dom g i^i dom h ) ) ` a ) =
                        ( G `
                          ( ( g |` ( dom g i^i dom h ) ) |`
                              Pred ( R , ( dom g i^i dom h ) , a ) ) ) ) ) $=
      ( vb vc wa cfv wceq wral wss wex wcel cdm cin cres wfn cpred wfun wfrlem2
      cv funfn sylib fnresin1 syl adantr inss1 sseli w3a wi wfrlem1 abeq2i fndm
      raleqdv biimpar ra4 3adant2 exlimiv sylbi imp adantlr fvres adantl resres
      syl5 predss sseqin2 mpbi 3an6 eeanv bitri ssinss1 ad2antrr hbra1 hban wel
      2exbii syl5com anim12d ssin biimpi syl6com ralrimi ad2ant2l jca ineqan12d
      inss2 sseq1 sseq2 raleqbi1dv anbi12d imbi2d mpbiri 3adant3 sylbir syl2anb
      wb exlimivv simpr preddowncl sylc syl5eq reseq2d fveq2d 3eqtr4d ralrimiva
      ) GUIZDUAZHUIZDUAZOZXOXOUBZXQUBZUCZUDZYBUEZJUIZYCPZYCYBEYEUFZUDZIPZQZJYBR
      XPYDXRXPXOXTUEZYDXPXOUGYKABCDEFGILUHXOUJUKXTYAXOULUMUNXSYJJYBXSYEYBUAZOZY
      EXOPZXOCEYEUFZUDZIPZYFYIXPYLYNYQQZXRXPYLYRYLYEXTUAZXPYRYBXTYEXTYAUOUPXPXO
      MUIZUEZYTCSZYOYTSZJYTRZOZYRJYTRZUQZMTZYSYRURZUUHGDABMJCDEFGILUSUTZUUGUUIM
      UUAUUFUUIUUEUUAUUFOYRJXTRZUUIUUAUUKUUFUUAYRJXTYTYTXOVAZVBVCYRJXTVDUMVEVFV
      GVMVHVIYLYFYNQXSYEYBXOVJVKYMYHYPIYMYHXOYBYGUCZUDYPXOYBYGVLYMUUMYOXOYMUUMY
      GYOYGYBSUUMYGQYBEYEVNYGYBVOVPYMYBCSZYOYBSZJYBRZOZYLYGYOQXSUUQYLXPUUHXQNUI
      ZUEZUURCSZYOUURSZJUURRZOZYEXQPXQYOUDIPQJUURRZUQZNTZUUQXRUUJUVFHDABNJCDEFH
      ILUSUTUUHUVFOZUUAUUSOZUUEUVCOZUUFUVDOZUQZNTMTZUUQUVLUUGUVEOZNTMTUVGUVKUVM
      MNUUAUUSUUEUVCUUFUVDVQWEUUGUVEMNVRVSUVKUUQMNUVHUVIUUQUVJUVHUVIUUQUVHUVIUU
      QURZUVIYTUURUCZCSZYOUVOSZJUVORZOZURZUVIUVPUVRUUBUVPUUDUVCYTUURCVTWAUUDUVB
      UVRUUBUUTUUDUVBOZUVQJUVOUUDUVBJUUCJYTWBUVAJUURWBWCYEUVOUAZUWAUUCUVAOZUVQU
      WBUUDUUCUVBUVAUWBJMWDUUDUUCUVOYTYEYTUURUOUPUUCJYTVDWFUWBJNWDUVBUVAUVOUURY
      EYTUURWOUPUVAJUURVDWFWGUWCUVQYOYTUURWHWIWJWKWLWMUVHYBUVOQZUVNUVTXEUUAUUSX
      TYTYAUURUULUURXQVAWNUWDUUQUVSUVIUWDUUNUVPUUPUVRYBUVOCWPUUOUVQJYBUVOYBUVOY
      OWQWRWSWTUMXAVHXBXFXCXDUNXSYLXGJCYBEYEXHXIXJXKXJXLXMXNWM $.
      $( [21-Apr-2011] $)
  $}

  ${
    $d A a f g h x y $.  $d B a $.  $d G a f g h x y $.  $d R a f g h x y $.
    $d g h u v x $.
    wfrlem5.1 $e |- R We A $.
    wfrlem5.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem5.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  The values of two acceptable
       functions agree within their domains. $)
    wfrlem5 $p |- ( ( g e. B /\ h e. B ) ->
                     ( ( x g u /\ x h v ) -> u = v ) ) $=
      ( va cv wa cres wcel wceq wbr cdm cin weq vex breldm anim12i elin anandir
      sylibr brres anbi12i bitr4i biimpi mpdan wwe cvv wral wfn cfv wss wfrlem3
      cpred ssinss1 wess mpi setlikess mpan2 adantr wfrlem4 ancom incom reseq2i
      3syl fneq1i fneq2i bitri eleq2i fveq1i reseq1 ax-mp predeq2 eqtri eqeq12i
      jca fveq2i imbi12i ralbii2 3imtr4i wfr3g syl3anc breqd biimprd wi wfrlem2
      wfun funres wal wrel dffun2 simprbi ax-4 a4s syl sylan2d syl5 ) APZDPZIPZ
      UAZXGCPZJPZUAZQZXGXHXIXIUBZXLUBZUCZRZUAZXGXKXLXQRZUAZQZXIFSZXLFSZQZDCUDZX
      NXGXQSZYBXNXGXOSZXGXPSZQYGXJYHXMYIXGXHXIAUEZUFXGXKXLYJUFUGXGXOXPUHUJXNYGQ
      ZYBYKXJYGQZXMYGQZQYBXJXMYGUIXSYLYAYMXGXHXIXQDUEUKXGXKXLXQCUEUKULUMUNUOYEY
      AXGXKXRUAZXSYFYEYNYAYEXRXTXGXKYEXQGUPZXQGXGVCUQSAXQURZQZXRXQUSOPZXRUTXRXQ
      GYRVCZRKUTTOXQURQXTXQUSZYRXTUTZXTYSRZKUTZTZOXQURZQZXRXTTYCYQYDYCXOEVAXQEV
      AZYQABEFGHIKNVBXOXPEVDUUGYOYPUUGEGUPYOLXQEGVEVFUUGEGXGVCUQSAEURYPMAXQEGVG
      VHWEVNVIABEFGHIJKOLNVJYDYCQXLXPXOUCZRZUUHUSZYRUUIUTZUUIUUHGYRVCZRZKUTZTZO
      UUHURZQYEUUFABEFGHJIKOLNVJYCYDVKYTUUJUUEUUPYTUUIXQUSUUJXQXTUUIXQUUHXLXOXP
      VLZVMZVOXQUUHUUIUUQVPVQUUDUUOOXQUUHYRXQSYRUUHSUUDUUOXQUUHYRUUQVRUUAUUKUUC
      UUNYRXTUUIUURVSUUBUUMKUUBUUIYSRZUUMXTUUITUUBUUSTUURXTUUIYSVTWAYSUULUUIXQU
      UHTYSUULTUUQXQUUHGYRWBWAVMWCWFWDWGWHULWIAOXQGXRXTKWJWKWLWMYCXSYNQYFWNZYDY
      CXIWPXRWPZUUTABEFGHIKNWOXQXIWQUVAUUTCWRZDWRZAWRZUUTUVAXRWSUVDADCXRWTXAUVC
      UUTAUVBUUTDUUTCXBXCXCXDVNVIXEXF $.
      $( [21-Apr-2011] $)
  $}

  ${
    $d A f g w x y z $.  $d B g $.  $d G f g w x y z $.  $d R f g w x y z $.
    $d X g w z $.
    wfrlem6.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem6.2 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  The union of all acceptable functions
       is a relationship. $)
    wfrlem6 $p |- Rel F $=
      ( vg cuni wceq wrel cv reluni wcel wfun wfrlem2 funrel syl mprgbir mpbiri
      releq ax-mp ) GDLZMZGNZJUGUHUFNZUIKOZNZKDKDPUJDQUJRUKABCDEFKHISUJTUAUBGUF
      UDUCUE $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  The domain of ` F ` is a subclass of
       ` A ` . $)
    wfrlem7 $p |- dom F C_ A $=
      ( vg cdm cv ciun cuni dmeqi dmuni eqtri wss iunss wfrlem3 mprgbir eqsstri
      ) GLZKDKMLZNZCUDDOZLUFGUGJPKDQRUFCSUECSKDKDUECTABCDEFKHIUAUBUC $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  Compute the prececessor class for an
       ` R ` minimal element of ` ( A \ dom F ) ` . $)
    wfrlem8 $p |- ( Pred ( R , ( A \ dom F ) , X ) = (/) <->
                   Pred ( R , A , X ) = Pred ( R , dom F , X ) ) $=
      ( cdm cdif cpred c0 wceq wss wa preddif eqeq1i wfrlem7 predpredss biantru
      ssdif0 ax-mp 3bitr2i eqss bitr4i ) CGLZMEINZOPZCEINZUIEINZQZUMULQZRZULUMP
      UKULUMMZOPUNUPUJUQOCUIEISTULUMUDUOUNUICQUOABCDEFGHJKUAUICEIUBUEUCUFULUMUG
      UH $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  If ` X e. dom F ` , then its
       predecessor class is a subset of ` dom F ` . $)
    wfrlem9 $p |- ( X e. dom F -> Pred ( R , A , X ) C_ dom F ) $=
      ( vg vz vw cv cdm wcel wrex cpred wss ciun wfn wral cfv cres wceq w3a wex
      wa wi wfrlem1 abeq2i predeq3 sseq1d rcla4cv adantl wb fndm eleq2d imbi12d
      sseq2d adantr mpbird adantrl 3adant3 exlimiv sylbi reximia ssiun syl cuni
      dmeqi dmuni eqtri eleq2i eliun bitri sseq2i 3imtr4i ) ILOZPZQZLDRZCEISZLD
      WAUAZTZIGPZQZWDWGTWCWDWATZLDRWFWBWILDVTDQVTMOZUBZWJCTZCENOZSZWJTZNWJUCZUI
      ZWMVTUDVTWNUEHUDUFNWJUCZUGZMUHZWBWIUJZWTLDABMNCDEFLHJUKULWSXAMWKWQXAWRWKW
      PXAWLWKWPUIXAIWJQZWDWJTZUJZWPXDWKWOXCNIWJWMIUFWNWDWJCEWMIUMUNUOUPWKXAXDUQ
      WPWKWBXBWIXCWKWAWJIWJVTURZUSWKWAWJWDXEVAUTVBVCVDVEVFVGVHLDWAWDVIVJWHIWEQW
      CWGWEIWGDVKZPWEGXFKVLLDVMVNZVOLIDWAVPVQWGWEWDXGVRVS $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A f w x y z $.  $d F w $.  $d G f x y $.  $d R f w x y $.
    wfrlem10.1 $e |- R We A $.
    wfrlem10.2 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem10.3 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  When ` z ` is an ` R ` minimal
       element of ` ( A \ dom F ) ` , then its predecessor class is equal to
       ` dom F ` . $)
    wfrlem10 $p |- ( ( z e. ( A \ dom F ) /\
                     Pred ( R , ( A \ dom F ) , z ) = (/) ) ->
                   Pred ( R , A , z ) = dom F ) $=
      ( vw cv wcel cpred wceq wa adantl wn cdm c0 wfrlem8 biimpi wss predss a1i
      cdif wi wbr simpr weq eleq1 biimprd eldifn syl6com con2d imp wfrlem9 ssel
      con3d syl5com adantr mpd wb eldifi elpredg ancoms sylan mtbid w3o wfrlem7
      sseli wor wwe weso ax-mp solin syl2an ecase23d cvv vex elpred sylanbrc ex
      mpan ssrdv eqssd eqtrd ) CNZDHUAZUHZOZWLFWJPUBQZRZDFWJPZWKFWJPZWKWNWPWQQZ
      WMWNWRABDEFGHIWJKLUCUDSWOWQWKWQWKUEWOWKFWJUFUGWOMWKWQWMMNZWKOZWSWQOZUIWNW
      MWTXAWMWTRZWTWSWJFUJZXAWMWTUKXBXCMCULZWJWSFUJZWMWTXDTWMXDWTXDWMWSWLOZWTTX
      DXFWMWSWJWLUMUNWSDWKUOUPUQURXBWJDFWSPZOZXEXBXGWKUEZXHTZWTXIWMABDEFGHIWSKL
      USSWMXIXJUIWTWMWJWKOZTXIXJWJDWKUOXIXHXKXGWKWJUTVAVBVCVDWMWJDOZWTXHXEVEZWJ
      DWKVFZWTXLXMDWKFWSWJVGVHVIVJWMXLWSDOZXCXDXEVKZWTXNWKDWSABDEFGHIKLVLVMXOXL
      XPDFVNZXOXLRXPDFVOXQJDFVPVQDWSWJFVRWFVHVSVTWJWAOXAWTXCRVECWBWKWAFWJWSMWBW
      CVQWDWEVCWGWHWI $.
      $( [21-Apr-2011] $)

  $}


  ${
    $d A f g h u v x y z $.  $d B g h $.  $d F u v x z $.  $d G f g h x y z $.
    $d R f g h x y z $.
    wfrlem11.1 $e |- R We A $.
    wfrlem11.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem11.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem11.4 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  The union of all acceptable functions
       is a function. $)
    wfrlem11 $p |- Fun F $=
      ( vu vv vg vh cv wa wcel wex wfun wrel wbr weq wi wal dffun2 wfrlem6 cuni
      eleq2i eluni bitri df-br anbi1i exbii 3bitr4i anbi12i eeanv bitr4i impcom
      cop wfrlem5 an4s exlimivv sylbi ax-gen gen2 mpbir2an ) GUAGUBAQZMQZGUCZVI
      NQZGUCZRZMNUDZUEZNUFZMUFAUFAMNGUGABCDEFGHKLUHVQAMVPNVNVIVJOQZUCZVRDSZRZVI
      VLPQZUCZWBDSZRZRZPTOTZVOVNWAOTZWEPTZRWGVKWHVMWIVIVJVAZGSZWJVRSZVTRZOTZVKW
      HWKWJDUIZSWNGWOWJLUJOWJDUKULVIVJGUMWAWMOVSWLVTVIVJVRUMUNUOUPVIVLVAZGSZWPW
      BSZWDRZPTZVMWIWQWPWOSWTGWOWPLUJPWPDUKULVIVLGUMWEWSPWCWRWDVIVLWBUMUNUOUPUQ
      WAWEOPURUSWFVOOPVSWCVTWDVOVTWDRVSWCRVOABNMCDEFOPHIJKVBUTVCVDVEVFVGVH $.
      $( [21-Apr-2011] $)


    ${
      $d F f $.
      $( Lemma for well-founded recursion.  Here, we compute the value of ` F `
         (the union of all acceptable functions). $)
      wfrlem12 $p |- ( y e. dom F ->
                    ( F ` y ) = ( G ` ( F |` Pred ( R , A , y ) ) ) ) $=
        ( vz cv wcel cfv wceq wss wa wi cdm cop wex cpred cres vex wfn wral w3a
        eldm2 unieqi eqtri eleq2i eluniab bitri abeq2i elssuni syl6sseqr sylbir
        cab cuni wel fnop ex ra4 impcom fndm sseq2d eleq2d anbi12d biimprd wfun
        exp3a wfrlem11 funssfv 3adant3l fun2ssres 3adant3r fveq2d mp3an1 expcom
        eqeq12d com23 syl6com com34 sylcom adantl com14 syl7 exp4a pm2.43d syld
        com12 3impd exlimdv mpdi imp exlimiv sylbi ) BNZGUAOWTMNZUBZGOZMUCWTGPZ
        GCEWTUDZUEZHPZQZMWTGBUFUJXCXHMXCXBFNZOZXIANZUGZXKCRZXEXKRZBXKUHZSZWTXIP
        ZXIXEUEZHPZQZBXKUHZUIZAUCZSZFUCZXHXCXBYCFUTZVAZOYEGYGXBGDVAZYGLDYFKUKUL
        UMYCFXBUNUOYDXHFXJYCXHXJYCXIGRZXHYCXIDOZYIYCFDKUPYJXIYHGXIDUQLURUSXJYBY
        IXHTZAXJXLXPYAYKXLXJXPYAYKTTZXLXJBAVBZYLXLXJYMXKWTXAXIVCVDXLYMYAXPYKXLY
        MYAXPYKTZTXLYMYMYAYNYMYASXTXLYMYNYAYMXTXTBXKVEVFXPYMXTXLYKXOYMXTXLYKTTZ
        TXMXOYMXNYOXNBXKVEYMXNXLXTYKYMXNXLXTYKTZXNXLSYMXEXIUAZRZWTYQOZSZYPXLXNY
        MYTTXLXNYMYTXLYTXNYMSXLYRXNYSYMXLYQXKXEXKXIVGZVHXLYQXKWTUUAVIVJVKVMVFYT
        YIXTXHYIYTXTXHTZGVLZYIYTUUBABCDEFGHIJKLVNUUCYIYTUIZXHXTUUDXDXQXGXSUUCYI
        YSXDXQQYRWTGXIVOVPUUDXFXRHUUCYIYRXFXRQYSXEGXIVQVRVSWBVKVTWAWCWDVMWEWFWG
        WHWIWJWKWEWLWMWNWOWPWQWRWSWRWS $.
        $( [21-Apr-2011] $)
    $}
  $}

  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y $.  $d R f x y z $.
    $d C f x y $.
    wfrlem13.1 $e |- R We A $.
    wfrlem13.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem13.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                           ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem13.4 $e |- F = U. B $.
    wfrlem13.5 $e |- C = ( F u.
                      { <. z , ( G ` ( F |` Pred ( R , A , z ) ) ) >. } ) $.
    $( Lemma for well-founded recursion.  From here through ~ wfrlem16 , we aim
       to prove that ` dom F = A ` .  We do this by supposing that there is an
       element ` z ` of ` A ` that is not in ` dom F ` .  We then define ` C `
       by extending ` dom F ` with the appropriate value at ` z ` .  We then
       show that ` z ` cannot be an ` R ` minimal element of
       ` ( A \ dom F ) ` , meaning that ` ( A \ dom F ) ` must be empty, so
       ` dom F = A ` .  Here, we show that ` C ` is a function extending the
       domain of ` F ` by one. $)
    wfrlem13 $p |- ( z e. ( A \ dom F ) -> C Fn ( dom F u. { z } ) ) $=
      ( cdm cun wfun wceq c0 cv cdif wcel cpred cfv cop csn wa wfn cin wfrlem11
      cres vex funsn pm3.2i dmsnop ineq2i wn eldifn disjsn sylibr funun sylancr
      syl5eq dmun uneq2i eqtri jctir fneq1i df-fn bitri ) CUAZDIPZUBUCZIVLIDGVL
      UDULJUEZUFUGZQZRZVQPZVMVLUGZQZSZUHZFWAUIZVNVRWBVNIRZVPRZUHVMVPPZUJZTSVRWE
      WFABDEGHIJKLMNUKVLVOCUMUNUOVNWHVMVTUJZTWGVTVMVLVOUPZUQVNVLVMUCURWITSVLDVM
      USVMVLUTVAVDIVPVBVCVSVMWGQWAIVPVEWGVTVMWJVFVGVHWDVQWAUIWCWAFVQOVIVQWAVJVK
      VA $.
      $( [21-Apr-2011] $)


    $( Lemma for well-founded recursion.  Compute the value of ` C ` . $)
    wfrlem14 $p |- ( z e. ( A \ dom F ) ->
                     ( y e. ( dom F u. { z } ) ->
                       ( C ` y ) = ( G ` ( C |` Pred ( R , A , y ) ) ) ) ) $=
      ( wcel cfv cres wceq ax-mp cv cdm cdif csn cun wfn cpred wi wfrlem13 elun
      weq wo elsn orbi2i bitri wa wfrlem12 wfun wb fnfun wss cop ssun1 sseqtr4i
      w3a funssfv wfrlem9 fun2ssres syl3an3 fveq2d eqeq12d mp3an2 sylan syl5ibr
      ex pm2.43d ssun2 vex snid sselii c0 reseq1 resundir eqtri wn wfr wwe wefr
      predfrirr ressnop0 uneq2i un0 3eqtri fveq2i opeq2i opex elsnc mpbir elun2
      eleqtrri fvex fnopfvb mpbiri mpan2 predeq3 reseq2d syl5ibrcom jaod syl5bi
      fveq2 syl ) CUAZDIUBZUCPFXMXLUDZUEZUFZBUAZXOPZXQFQZFDGXQUGZRZJQZSZUHABCDE
      FGHIJKLMNOUIXRXQXMPZBCUKZULZXPYCXRYDXQXNPZULYFXQXMXNUJYGYEYDBXLUMUNUOXPYD
      YCYEXPYDYCXPYDYDYCUHYDYCXPYDUPXQIQZIXTRZJQZSZABDEGHIJKLMNUQXPFURZYDYCYKUS
      ZXOFUTYLIFVAZYDYMIIXLIDGXLUGZRZJQZVBZUDZUEZFIYSVCOVDYLYNYDVEZXSYHYBYJXQFI
      VFUUAYAYIJYDYLYNXTXMVAYAYISABDEGHIJXQMNVGXTFIVHVIVJVKVLVMVNVOVPXPYCYEXLFQ
      ZFYORZJQZSZXPXLXOPZUUEXNXOXLXNXMVQXLCVRVSVTXPUUFUPUUEXLUUDVBZFPUUGYTFUUGY
      SPZUUGYTPUUHUUGYRSUUDYQXLUUCYPJUUCYPYSYORZUEZYPWAUEYPUUCYTYORZUUJFYTSUUCU
      UKSOFYTYOWBTIYSYOWCWDUUIWAYPXLYOPWEZUUIWASDGWFZUULDGWGUUMKDGWHTDGXLWITXLY
      QYOWJTWKYPWLWMWNWOUUGYRXLUUDWPWQWRUUGYSIWSTOWTXOXLUUDFUUCJXAXBXCXDYEXSUUB
      YBUUDXQXLFXJYEYAUUCJYEXTYOFDGXQXLXEXFVJVKXGXHXIXK $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  When ` z ` is ` R ` minimal, ` C ` is
       an acceptable function. $)
    wfrlem15 $p |- ( ( z e. ( A \ dom F ) /\
                       Pred ( R , ( A \ dom F ) , z ) = (/) ) ->
                     C e. B ) $=
      ( wcel wceq wss wral cvv cv cdm cdif cpred c0 wa wfn cfv cres w3a wex csn
      wfrlem10 eldifi setlikespec sylancl adantr eqeltrrd snex wfrlem13 wfrlem7
      cun unexg snssd unss biimpi sylancr weq wo elun elsn orbi2i bitri wfrlem9
      ssun3 syl a1i ssun1 sseq1d mpbiri predeq3 syl5ibrcom jaod syl5bi ralrimiv
      wi jca wfrlem14 3jca fneq2 sseq1 sseq2 raleqbi1dv anbi12d raleq 3anbi123d
      cla4egv sylc mpan2 fnex sylan2 syl2anc fneq1 fveq1 reseq1 eqeq12d ralbidv
      wb fveq2d 3anbi13d exbidv elab2g mpbird ) CUAZDIUBZUCZPZXPGXNUDUEQZUFZFEP
      ZFAUAZUGZYADRZDGBUAZUDZYARZBYASZUFZYDFUHZFYEUIZJUHZQZBYASZUJZAUKZXSXOXNUL
      ZVBZTPZFYQUGZYQDRZYEYQRZBYQSZUFZYLBYQSZUJZYOXSXOTPZYPTPZYRXSDGXNUDZXOTABC
      DEGHIJKMNUMZXQUUHTPZXRXQXNDPDGYAUDTPADSUUJXNDXOUNZLADGXNUOUPUQURZXNUSZXOY
      PTTVCZUPXSYSUUCUUDXQYSXRABCDEFGHIJKLMNOUTUQZXSYTUUBXQYTXRXQXODRZYPDRZYTAB
      DEGHIJMNVAXQXNDUUKVDUUPUUQUFYTXOYPDVEVFVGUQXSUUABYQYDYQPZYDXOPZBCVHZVIZXS
      UUAUURUUSYDYPPZVIUVAYDXOYPVJUVBUUTUUSBXNVKVLVMXSUUSUUAUUTUUSUUAWFXSUUSYEX
      ORUUAABDEGHIJYDMNVNYEXOYPVOVPVQXSUUAUUTUUHYQRZXSUVCXOYQRXOYPVRXSUUHXOYQUU
      IVSVTUUTYEUUHYQDGYDXNWAVSWBWCWDWEWGXQUUDXRXQYLBYQABCDEFGHIJKLMNOWHWEUQWIY
      NUUEAYQTYAYQQZYBYSYHUUCYMUUDYAYQFWJUVDYCYTYGUUBYAYQDWKYFUUABYAYQYAYQYEWLW
      MWNYLBYAYQWOWPWQWRXSFTPZXTYOXHXSYSUUFUVEUUOUULUUFYSYRUVEUUFUUGYRUUMUUNWSY
      QTFWTXAXBHUAZYAUGZYHYDUVFUHZUVFYEUIZJUHZQZBYASZUJZAUKYOHFETUVFFQZUVMYNAUV
      NUVGYBUVLYMYHYAUVFFXCUVNUVKYLBYAUVNUVHYIUVJYKYDUVFFXDUVNUVIYJJUVFFYEXEXIX
      FXGXJXKMXLVPXM $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  If ` z ` is ` R ` minimal in
       ` ( A \ dom F ) ` , then ` C ` is acceptable and thus a subset of
       ` F ` , but ` dom C ` is bigger than ` dom F ` .  Thus, ` z ` cannot be
       minimal, so ` ( A \ dom F ) ` must be empty, and (due to ~ wfrlem7 ),
       ` dom F = A ` . $)
    wfrlem16 $p |- dom F = A $=
      ( cdm wss c0 wcel cun wfrlem7 cdif wceq cv cpred wrex eldifn wa csn ssun2
      vex snid sselii cres cfv cop dmeqi dmsnop uneq2i 3eqtri eleqtrri wfrlem15
      dmun cuni elssuni syl syl6sseqr dmss sseld mpi mtand nrex wne df-ne difss
      wn tz6.26i mpan sylbir mt3 ssdif0 mpbir eqssi ) IPZDABDEGHIJMNUADWDQDWDUB
      ZRUCZWFWEGCUDZUERUCZCWEUFZWHCWEWGWESZWHWGWDSZWGDWDUGWJWHUHZWGFPZSWKWGWDWG
      UIZTZWMWNWOWGWNWDUJWGCUKULUMWMIWGIDGWGUEUNJUOZUPUIZTZPWDWQPZTWOFWROUQIWQV
      CWSWNWDWGWPURUSUTVAWLWMWDWGWLFIQWMWDQWLFEVDZIWLFESFWTQABCDEFGHIJKLMNOVBFE
      VEVFNVGFIVHVFVIVJVKVLWFVPWERVMZWIWERVNWEDQXAWIDWDVOACDWEGKLVQVRVSVTDWDWAW
      BWC $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y $.  $d R f x y z $.
    wfr1.1 $e |- R We A $.
    wfr1.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr1.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr1.4 $e |- F = U. B $.
    $( The Principle of Well-Founded Recursion, part 1 of 3.  We start with an
       arbitrary function ` G ` and a class of "acceptable" functions ` B ` .
       Then, using a base class ` A ` and a well-ordering ` R ` of ` A ` , we
       define a function ` F ` .  This function is said to be defined by
       "well-founded recursion."  The purpose of these three theorems is to
       demonstrate the properties of ` F ` .  We begin by showing that ` F ` is
       a function over ` A ` .  (Contributed by Scott Fenton, 22-Apr-2011.) $)
    wfr1 $p |- F Fn A $=
      ( vz wfn wfun cdm wceq df-fn wfrlem11 cv cpred cres cfv cop eqid wfrlem16
      csn cun mpbir2an ) GCNGOGPCQGCRABCDEFGHIJKLSABMCDGMTZGCEUJUAUBHUCUDUGUHZE
      FGHIJKLUKUEUFUI $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d A f w x y $.  $d F f w x y $.  $d G f x y $.  $d R f w x y $.  $d y z $.
    wfr2.1 $e |- R We A $.
    wfr2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr2.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr2.4 $e |- F = U. B $.
    $( The Principle of Well-Founded Recursion, part 2 of 3.  Next, we show
       that the value of ` F ` at any ` z e. A ` is ` G ` recursively applied
       to all "previous" values of ` F ` .  (Contributed by Scott Fenton,
       18-Apr-2011.) $)
    wfr2 $p |- ( z e. A ->
                  ( F ` z ) = ( G ` ( F |` Pred ( R , A , z ) ) ) ) $=
      ( vw cv cfv cpred cres wceq wcel weq fveq2 predeq3 reseq2d fveq2d eqeq12d
      cdm cop csn cun eqid wfrlem16 eleq2i wfrlem12 sylbir vtoclga ) BOZHPZHDFU
      QQZRZIPZSZCOZHPZHDFVCQZRZIPZSBVCDBCUAZURVDVAVGUQVCHUBVHUTVFIVHUSVEHDFUQVC
      UCUDUEUFUQDTUQHUGZTVBVIDUQABNDEHNOZHDFVJQRIPUHUIUJZFGHIJKLMVKUKULUMABDEFG
      HIJKLMUNUOUP $.
      $( [18-Apr-2011] $)

    ${
      $d A z $.  $d F z $.  $d G z $.  $d R z $.  $d X z $.
      $( Generalize ~ wfr2 to class arguments.  (Contributed by Scott Fenton,
         6-Aug-2013.) $)
      wfr2c $p |- ( X e. A ->
                  ( F ` X ) = ( G ` ( F |` Pred ( R , A , X ) ) ) ) $=
        ( vz cv cfv cpred cres wceq fveq2 predeq3 reseq2d eqeq12d wfr2 vtoclga
        fveq2d ) NOZGPZGCEUGQZRZHPZSIGPZGCEIQZRZHPZSNICUGISZUHULUKUOUGIGTUPUJUN
        HUPUIUMGCEUGIUAUBUFUCABNCDEFGHJKLMUDUE $.
        $( [6-Aug-2013] $)
    $}

  $}


  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y z $.  $d H x z $.
    $d R f x y z $.
    wfr3.1 $e |- R We A $.
    wfr3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr3.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr3.4 $e |- F = U. B $.
    $( The principle of Well-Founded Recursion, part 3 of 3.  Finally, we show
       that ` F ` is unique.  We do this by showing that any function ` H `
       with the same properties we proved of ` F ` in ~ wfr1 and ~ wfr2 is
       identical to ` F ` .  (Contributed by Scott Fenton, 18-Apr-2011.) $)
    wfr3 $p |- ( ( H Fn A /\
                    A. z e. A ( H ` z ) =
                            ( G ` ( H |` Pred ( R , A , z ) ) ) ) ->
                  F = H ) $=
      ( cv cpred wral wa cfv wceq wwe cvv wcel cres pm3.2i wfr1 wfr2 rgen wfr3g
      wfn mp3an12 ) DFUAZDFAOPUBUCADQZRHDUJZCOZHSHDFUOPZUDISTZCDQZRJDUJUOJSJUPU
      DISTCDQRHJTULUMKLUEUNURABDEFGHIKLMNUFUQCDABCDEFGHIKLMNUGUHUEACDFHJIUIUK
      $.
      $( [18-Apr-2011] $)
  $}


