$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Well-founded recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A w x y z $.  $d F w x y z $.  $d G w x y z $.  $d H w x y z $.
    $d R w x y z $.
    $( Functions defined by well founded recursion are identical up to
       relation, domain, and characteristic function. $)
    wfr3g $p |- ( ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                        ( F Fn A /\
                          A. y e. A ( F ` y ) =
                                    ( H ` ( F |` Pred ( R , A , y ) ) ) ) /\
                     ( G Fn A /\
                       A. y e. A ( G ` y ) =
                                 ( H ` ( G |` Pred ( R , A , y ) ) ) ) )
                   -> F = G ) $=
      ( vz vw wwe cv cpred cvv wcel wral wa wfn cfv cres wceq w3a wi weq fveq2
      eqeq12d imbi2d predeq3 reseq2 syl fveq2d anbi12d rcla4va wss wb predss
      fvreseq mpan2 biimpar eqcomd eqtr3 ancoms ex expimpd com12 exp3a com23
      imp3a a2d ax-17 ra5 syl5 wfis2g r19.21 sylib r19.26 sylan2br an4s 3impib
      eqid jctil eqfnfv ad2ant2r 3adant1 mpbird ) CDJCDAKLMNACOPZECQZBKZERZECDW
      GLZSZGRZTZBCOZPZFCQZWGFRZFWISZGRZTZBCOZPZUAZEFTZCCTZHKZERZXEFRZTZHCOZPZXB
      XIXDWEWNXAXIWNXAPWEXIWFWOWMWTWEXIUBZWFWOPZWLWSPZBCOZXKWMWTPWEXLXNPZXIWEXO
      XHUBZHCOXOXIUBXPXOIKZERZXQFRZTZUBZAHICDHIUCZXHXTXOYBXFXRXGXSXEXQEUDXEXQFU
      DUEUFXECNZXOXTICDXELZOZUBXPYAIYDOYCXOYEXHYCXLXNYEXHUBZYCXNXLYFYCXNXLYFUBZ
      YCXNPXFEYDSZGRZTZXGFYDSZGRZTZPZYGXMYNBXECBHUCZWLYJWSYMYOWHXFWKYIWGXEEUDYO
      WJYHGYOWIYDTZWJYHTCDWGXEUGZWIYDEUHUIUJUEYOWPXGWRYLWGXEFUDYOWQYKGYOYPWQYKT
      YQWIYDFUHUIUJUEUKULYNXLYEXHXLYEPZYNXHYRYLYITZYNXHUBYRYKYHGYRYHYKXLYHYKTZY
      EXLYDCUMYTYEUNCDXEUOICYDEFUPUQURUSUJYSYJYMXHYSYJPXFYLTZYMXHUBYJYSUUAXFYLY
      IUTVAUUAYMXHXFXGYLUTVBUIVCUIVDVEUIVBVFVGVHXOXTIYDXOIVIVJVKVLXOXHHCXOHVIVM
      VNVDWLWSBCVOVPVQVDVRCVSVTWNXAXCXJUNZWEWFWOUUBWMWTHCCEFWAWBWCWD $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A f g w x y z $.  $d G f g w x y z $.  $d R f g w x y z $.
    wfrlem1.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  The final item we are interested in
       is the union of acceptable functions ` B ` .  This lemma just changes
       bound variables for later use. $)
    wfrlem1 $p |- B = { g |
                         E. z ( g Fn z /\
                                  ( z C_ A /\
                                    A. w e. z Pred ( R , A , w ) C_ z ) /\
                                  A. w e. z ( g ` w ) =
                                            ( G ` ( g |` Pred ( R , A , w ) ) )
                                ) } $=
      ( cv wfn wss cpred wral wa cfv cres wceq w3a wex cab weq fneq1 fveq1
      reseq1 fveq2d eqeq12d ralbidv 3anbi13d exbidv fneq2 sseq1 sseq2
      raleqbi1dv predeq3 sseq1d cbvralv syl6bb anbi12d raleq fveq2 reseq2 syl
      3anbi123d cbvexv cbvabv eqtri ) FHLZALZMZVKENZEGBLZOZVKNZBVKPZQZVNVJRZVJV
      OSZJRZTZBVKPZUAZAUBZHUCILZCLZMZWGENZEGDLZOZWGNZDWGPZQZWJWFRZWFWKSZJRZTZDW
      GPZUAZCUBZIUCKWEXAHIHIUDZWEWFVKMZVRVNWFRZWFVOSZJRZTZBVKPZUAZAUBXAXBWDXIAX
      BVLXCWCXHVRVKVJWFUEXBWBXGBVKXBVSXDWAXFVNVJWFUFXBVTXEJVJWFVOUGUHUIUJUKULXI
      WTACACUDZXCWHVRWNXHWSVKWGWFUMXJVMWIVQWMVKWGEUNXJVQVOWGNZBWGPWMVPXKBVKWGVK
      WGVOUOUPXKWLBDWGBDUDZVOWKWGEGVNWJUQZURUSUTVAXJXHXGBWGPWSXGBVKWGVBXGWRBDWG
      XLXDWOXFWQVNWJWFVCXLXEWPJXLVOWKTXEWPTXMVOWKWFVDVEUHUIUSUTVFVGUTVHVI $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  An acceptable function is a
       function. $)
    wfrlem2 $p |- ( g e. B -> Fun g ) $=
      ( vz vw cv wcel wfn wss cpred wral wa cfv cres wceq w3a wex wfun wfrlem1
      abeq2i fnfun 3ad2ant1 19.23aiv sylbi ) GLZDMUKJLZNZULCOCEKLZPZULOKULQRZUN
      UKSUKUOTHSUAKULQZUBZJUCZUKUDZUSGDABJKCDEFGHIUEUFURUTJUMUPUTUQULUKUGUHUIUJ
      $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  An acceptable function's domain is a
       subset of ` A ` . $)
    wfrlem3 $p |- ( g e. B -> dom g C_ A ) $=
      ( vz vw cv wcel wfn wss cpred wral wa cfv cres wceq w3a wex cdm wfrlem1
      abeq2i fndm sseq1d biimpar adantrr 3adant3 19.23aiv sylbi ) GLZDMUNJLZNZU
      OCOZCEKLZPZUOOKUOQZRZURUNSUNUSTHSUAKUOQZUBZJUCZUNUDZCOZVDGDABJKCDEFGHIUEU
      FVCVFJUPVAVFVBUPUQVFUTUPVFUQUPVEUOCUOUNUGUHUIUJUKULUM $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A a b c f g h x y $.  $d B a $.  $d G a b c f g h x y $.
    $d R a b c f g h x y $.
    wfrlem4.1 $e |- R We A $.
    wfrlem4.2 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  Properties of the restriction of an
       acceptable function to the domain of another one. $)
    wfrlem4 $p |- ( ( g e. B /\ h e. B ) ->
                     ( ( g |` ( dom g i^i dom h ) ) Fn ( dom g i^i dom h ) /\
                       A. a e. ( dom g i^i dom h )
                        ( ( g |` ( dom g i^i dom h ) ) ` a ) =
                        ( G `
                          ( ( g |` ( dom g i^i dom h ) ) |`
                              Pred ( R , ( dom g i^i dom h ) , a ) ) ) ) ) $=
      ( vb vc cv wcel wa cdm cin cres wfn cfv cpred wceq wral wfun wfrlem2
      funfn sylib fnresin1 syl adantr wss w3a wex wi wfrlem1 abeq2i fndm
      raleqdv biimpar ra4 3adant2 19.23aiv sylbi inss1 sseli syl5 imp adantlr
      fvres adantl 3an6 2exbii eeanv bitri ssinss1 ad2antrr hbra1 hban wel
      syl5com inss2 anim12d ssin biimpi syl6com r19.21ai ad2ant2l jca wb
      ineqan12d sseq1 sseq2 raleqbi1dv anbi12d imbi2d mpbiri 3adant3 19.23aivv
      sylbir syl2anb simpr preddowncl sylc predss sseqin2 mpbi syl5eq reseq2
      resres fveq2d 3eqtr4d r19.21aiva ) GOZDPZHOZDPZQZXOXORZXQRZSZTZYBUAZJOZYC
      UBZYCYBEYEUCZTZIUBZUDZJYBUEXPYDXRXPXOXTUAZYDXPXOUFYKABCDEFGILUGXOUHUIXTYA
      XOUJUKULXSYJJYBXSYEYBPZQZYEXOUBZXOCEYEUCZTZIUBZYFYIXPYLYNYQUDZXRXPYLYRXPY
      EXTPZYRYLXPXOMOZUAZYTCUMZYOYTUMZJYTUEZQZYRJYTUEZUNZMUOZYSYRUPZUUHGDABMJCD
      EFGILUQURZUUGUUIMUUAUUFUUIUUEUUAUUFQYRJXTUEZUUIUUAUUKUUFUUAYRJXTYTYTXOUSZ
      UTVAYRJXTVBUKVCVDVEYBXTYEXTYAVFVGVHVIVJYLYFYNUDXSYEYBXOVKVLYMYHYPIYMXOYBY
      GSZTZYPYHYMUUMYOUDUUNYPUDYMYGYOUUMYMYBCUMZYOYBUMZJYBUEZQZYLYGYOUDXSUURYLU
      UHXQNOZUAZUUSCUMZYOUUSUMZJUUSUEZQZYEXQUBXQYOTIUBUDJUUSUEZUNZNUOZUURXPXRUU
      HUVGQZUUAUUTQZUUEUVDQZUUFUVEQZUNZNUOMUOZUURUVMUUGUVFQZNUOMUOUVHUVLUVNMNUU
      AUUTUUEUVDUUFUVEVMVNUUGUVFMNVOVPUVLUURMNUVIUVJUURUVKUVIUVJUURUVIUVJUURUPZ
      UVJYTUUSSZCUMZYOUVPUMZJUVPUEZQZUPZUVJUVQUVSUUBUVQUUDUVDYTUUSCVQVRUUDUVCUV
      SUUBUVAUUDUVCQZUVRJUVPUUDUVCJUUCJYTVSUVBJUUSVSVTYEUVPPZUWBUUCUVBQZUVRUWCU
      UDUUCUVCUVBUUDJMWAUUCUWCUUCJYTVBUVPYTYEYTUUSVFVGWBUVCJNWAUVBUWCUVBJUUSVBU
      VPUUSYEYTUUSWCVGWBWDUWDUVRYOYTUUSWEWFWGWHWIWJUVIYBUVPUDZUVOUWAWKUUAUUTXTY
      TYAUUSUULUUSXQUSWLUWEUURUVTUVJUWEUUOUVQUUQUVSYBUVPCWMUUPUVRJYBUVPYBUVPYOW
      NWOWPWQUKWRVIWSWTXAUUJUVGHDABNJCDEFHILUQURXBULXSYLXCJCYBEYEXDXEYGYBUMUUMY
      GUDYBEYEXFYGYBXGXHXIUUMYOXOXJUKXOYBYGXKXIXLXMXNWJ $.
      $( [21-Apr-2011] $)
  $}

  ${
    $d A a f g h x y $.  $d B a $.  $d G a f g h x y $.  $d R a f g h x y $.
    $d g h u v x $.
    wfrlem5.1 $e |- R We A $.
    wfrlem5.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem5.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    $( Lemma for well-founded recursion.  The values of two acceptable
       functions agree within their domains. $)
    wfrlem5 $p |- ( ( g e. B /\ h e. B ) ->
                     ( ( x g u /\ x h v ) -> u = v ) ) $=
      ( va cv wcel wa cdm cin cres wbr weq wi wfun wfrlem2 funres wal wrel
      dffun2 simprbi ax-4 a4s syl 3syl adantr wwe cpred cvv wral wfn cfv wceq
      wfr3g wss wfrlem3 ssinss1 wess mpi setlikess mpan2 jca wfrlem4 ancom
      incom reseq2 ax-mp fneq1i fneq2i bitri eleq2i fveq1i reseq1 predeq2
      eqtri fveq2i eqeq12i imbi12i ralbii2 anbi12i 3imtr4i syl3ancOLD breqd
      biimprd sylan2d visset breldm anim12i elin sylibr anandir brres bitr4i
      biimpi mpdan syl5 ) IPZFQZJPZFQZRZAPZDPZXGXGSZXISZTZUAZUBZXLCPZXIXPUAZUBZ
      RZDCUCZXLXMXGUBZXLXSXIUBZRZXKXRXLXSXQUBZYCYAXHXRYGRYCUDZXJXHXGUEXQUEZYHAB
      EFGHIKNUFXPXGUGYIYHCUHZDUHZAUHZYHYIXQUIYLADCXQUJUKYKYHAYJYHDYHCULUMUMUNUO
      UPXKYGYAXKXQXTXLXSXPGUQZXPGXLURUSQAXPUTZRZXQXPVAOPZXQVBXQXPGYPURZUAKVBVCO
      XPUTRXTXPVAZYPXTVBZXTYQUAZKVBZVCZOXPUTZRZXQXTVCXKAOXPGXQXTKVDXHYOXJXHXNEV
      EXPEVEZYOABEFGHIKNVFXNXOEVGUUEYMYNUUEEGUQYMLXPEGVHVIUUEEGXLURUSQAEUTYNMAX
      PEGVJVKVLUOUPABEFGHIJKOLNVMXJXHRXIXOXNTZUAZUUFVAZYPUUGVBZUUGUUFGYPURZUAZK
      VBZVCZOUUFUTZRXKUUDABEFGHJIKOLNVMXHXJVNYRUUHUUCUUNYRUUGXPVAUUHXPXTUUGXPUU
      FVCZXTUUGVCZXNXOVOZXPUUFXIVPVQZVRXPUUFUUGUUQVSVTUUBUUMOXPUUFYPXPQYPUUFQUU
      BUUMXPUUFYPUUQWAYSUUIUUAUULYPXTUUGUURWBYTUUKKYTUUGYQUAZUUKUUPYTUUSVCUURXT
      UUGYQWCVQYQUUJVCZUUSUUKVCUUOUUTUUQXPUUFGYPWDVQYQUUJUUGVPVQWEWFWGWHWIWJWKW
      LWMWNWOYFXLXPQZYBYFXLXNQZXLXOQZRUVAYDUVBYEUVCXLXMXGAWPZWQXLXSXIUVDWQWRXLX
      NXOWSWTYFUVARZYBUVEYDUVARZYEUVARZRYBYDYEUVAXAXRUVFYAUVGXLXMXGXPDWPXBXLXSX
      IXPCWPXBWJXCXDXEXF $.
      $( [21-Apr-2011] $)
  $}

  ${
    $d A f g w x y z $.  $d B g $.  $d G f g w x y z $.  $d R f g w x y z $.
    $d X g w z $.
    wfrlem6.1 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem6.2 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  The union of all acceptable functions
       is a relationship. $)
    wfrlem6 $p |- Rel F $=
      ( vg cuni wceq wrel cv reluni wcel wfun wfrlem2 funrel syl mprgbir releq
      mpbiri ax-mp ) GDLZMZGNZJUGUHUFNZUIKOZNZKDKDPUJDQUJRUKABCDEFKHISUJTUAUBGU
      FUCUDUE $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  The domain of ` F ` is a subclass of
       ` A ` . $)
    wfrlem7 $p |- dom F C_ A $=
      ( vg cdm cv ciun cuni dmeqi dmuni eqtri wss iunss wfrlem3 mprgbir
      eqsstri ) GLZKDKMLZNZCUDDOZLUFGUGJPKDQRUFCSUECSKDKDUECTABCDEFKHIUAUBUC $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  Compute the prececessor class for an
       ` R ` minimal element of ` ( A \ dom F ) ` . $)
    wfrlem8 $p |- ( Pred ( R , ( A \ dom F ) , X ) = (/) <->
                   Pred ( R , A , X ) = Pred ( R , dom F , X ) ) $=
      ( cdm cdif cpred c0 wceq wss wa preddif eqeq1i ssdif0 wfrlem7 predpredss
      ax-mp biantru 3bitr2i eqss bitr4i ) CGLZMEINZOPZCEINZUIEINZQZUMULQZRZULUM
      PUKULUMMZOPUNUPUJUQOCUIEISTULUMUAUOUNUICQUOABCDEFGHJKUBUICEIUCUDUEUFULUMU
      GUH $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  If ` X e. dom F ` , then its
       predecessor class is a subset of ` dom F ` . $)
    wfrlem9 $p |- ( X e. dom F -> Pred ( R , A , X ) C_ dom F ) $=
      ( vg vz vw cv cdm wcel wrex cpred ciun wss wfn wral wa cfv cres wceq w3a
      wex wi wfrlem1 abeq2i predeq3 sseq1d rcla4cv adantl wb fndm eleq2d
      sseq2d imbi12d adantr mpbird adantrl 3adant3 19.23aiv sylbi reximia
      ssiun syl cuni dmeqi dmuni eqtri eleq2i eliun bitri sseq2i 3imtr4i ) ILOZ
      PZQZLDRZCEISZLDWATZUAZIGPZQZWDWGUAWCWDWAUAZLDRWFWBWILDVTDQVTMOZUBZWJCUAZC
      ENOZSZWJUAZNWJUCZUDZWMVTUEVTWNUFHUEUGNWJUCZUHZMUIZWBWIUJZWTLDABMNCDEFLHJU
      KULWSXAMWKWQXAWRWKWPXAWLWKWPUDXAIWJQZWDWJUAZUJZWPXDWKWOXCNIWJWMIUGWNWDWJC
      EWMIUMUNUOUPWKXAXDUQWPWKWBXBWIXCWKWAWJIWJVTURZUSWKWAWJWDXEUTVAVBVCVDVEVFV
      GVHLDWAWDVIVJWHIWEQWCWGWEIWGDVKZPWEGXFKVLLDVMVNZVOLIDWAVPVQWGWEWDXGVRVS
      $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A f w x y z $.  $d F w $.  $d G f x y $.  $d R f w x y $.
    wfrlem10.1 $e |- R We A $.
    wfrlem10.2 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem10.3 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  When ` z ` is an ` R ` minimal
       element of ` ( A \ dom F ) ` , then its predecessor class is equal to
       ` dom F ` . $)
    wfrlem10 $p |- ( ( z e. ( A \ dom F ) /\
                     Pred ( R , ( A \ dom F ) , z ) = (/) ) ->
                   Pred ( R , A , z ) = dom F ) $=
      ( vw cv cdm cdif wcel cpred c0 wceq wa wfrlem8 biimpi adantl wss predss
      a1i wi wbr cvv wb visset elpred ax-mp simpr weq wn eleq1 biimprd eldifn
      syl6com con2d imp wfrlem9 ssel con3d syl5com adantr mpd elpredg ancoms
      eldifi sylan mtbid w3o wor wwe weso solin mpan wfrlem7 sseli syl2an
      ecase23d sylanbrc ex ssrdv eqssd eqtrd ) CNZDHOZPZQZWLFWJRSTZUAZDFWJRZWKF
      WJRZWKWNWPWQTZWMWNWRABDEFGHIWJKLUBUCUDWOWQWKWQWKUEWOWKFWJUFUGWOMWKWQWMMNZ
      WKQZWSWQQZUHWNWMWTXAXAWTWSWJFUIZWMWTUAZWJUJQXAWTXBUAUKCULWKUJFWJWSMULUMUN
      WMWTUOXCXBMCUPZWJWSFUIZWMWTXDUQWMXDWTXDWMWSWLQZWTUQXDXFWMWSWJWLURUSWSDWKU
      TVAVBVCXCWJDFWSRZQZXEXCXGWKUEZXHUQZWTXIWMABDEFGHIWSKLVDUDWMXIXJUHWTXIWJWK
      QZUQXJWMXIXHXKXGWKWJVEVFWJDWKUTVGVHVIWJDQZWTXHXEUKZWMWTXLXMDWKFWSWJVJVKWJ
      DWKVLZVMVNXLWSDQZXBXDXEVOZWMWTXOXLXPDFVPZXOXLUAXPDFVQXQJDFVRUNDWSWJFVSVTV
      KXNWKDWSABDEFGHIKLWAWBWCWDWEWFVHWGWHWI $.
      $( [21-Apr-2011] $)

  $}


  ${
    $d A f g h u v x y z $.  $d B g h $.  $d F u v x z $.  $d G f g h x y z $.
    $d R f g h x y z $.
    wfrlem11.1 $e |- R We A $.
    wfrlem11.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem11.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem11.4 $e |- F = U. B $.
    $( Lemma for well-founded recursion.  The union of all acceptable functions
       is a function. $)
    wfrlem11 $p |- Fun F $=
      ( vu vv vg vh wfun wrel cv wbr wa weq wi wal dffun2 wfrlem6 wcel wex cop
      cuni eleq2i eluni bitri df-br anbi1i exbii 3bitr4i anbi12i eeanv bitr4i
      wfrlem5 impcom an4s 19.23aivv sylbi ax-gen gen2 mpbir2an ) GQGRASZMSZGTZV
      INSZGTZUAZMNUBZUCZNUDZMUDAUDAMNGUEABCDEFGHKLUFVQAMVPNVNVIVJOSZTZVRDUGZUAZ
      VIVLPSZTZWBDUGZUAZUAZPUHOUHZVOVNWAOUHZWEPUHZUAWGVKWHVMWIVIVJUIZGUGZWJVRUG
      ZVTUAZOUHZVKWHWKWJDUJZUGWNGWOWJLUKOWJDULUMVIVJGUNWAWMOVSWLVTVIVJVRUNUOUPU
      QVIVLUIZGUGZWPWBUGZWDUAZPUHZVMWIWQWPWOUGWTGWOWPLUKPWPDULUMVIVLGUNWEWSPWCW
      RWDVIVLWBUNUOUPUQURWAWEOPUSUTWFVOOPVSWCVTWDVOVTWDUAVSWCUAVOABNMCDEFOPHIJK
      VAVBVCVDVEVFVGVH $.
      $( [21-Apr-2011] $)


    ${
      $d F f $.
      $( Lemma for well-founded recursion.  Here, we compute the value of ` F `
         (the union of all acceptable functions). $)
      wfrlem12 $p |- ( y e. dom F ->
                    ( F ` y ) = ( G ` ( F |` Pred ( R , A , y ) ) ) ) $=
        ( vz cv cdm wcel cop wex cfv cpred cres wceq visset eldm2 wfn wss wral
        wa w3a cab cuni unieqi eqtri eleq2i eluniab bitri abeq2i elssuni
        syl6ssr sylbir wi wel fnop ex ra4 fndm sseq2d eleq2d anbi12d biimprd
        exp3a impcom wfun wfrlem11 funssfv 3adant3l fun2ssres 3adant3r fveq2d
        eqeq12d mp3an1 expcom com23 syl6com com34 sylcom adantl com14 syl7
        exp4a pm2.43d syld com12 3impd 19.23adv mpdi imp 19.23aiv sylbi ) BNZGO
        PWTMNZQZGPZMRWTGSZGCEWTTZUAZHSZUBZMWTGBUCUDXCXHMXCXBFNZPZXIANZUEZXKCUFZ
        XEXKUFZBXKUGZUHZWTXISZXIXEUAZHSZUBZBXKUGZUIZARZUHZFRZXHXCXBYCFUJZUKZPYE
        GYGXBGDUKZYGLDYFKULUMUNYCFXBUOUPYDXHFXJYCXHXJYCXIGUFZXHYCXIDPZYIYCFDKUQ
        YJXIYHGXIDURLUSUTXJYBYIXHVAZAXJXLXPYAYKXLXJXPYAYKVAVAZXLXJBAVBZYLXLXJYM
        XKWTXAXIVCVDXLYMYAXPYKXLYMYAXPYKVAZVAXLYMYMYAYNXLYMXTYNYMYAUHXPYMXTXLYK
        XOYMXTXLYKVAVAZVAXMXOYMXNYOXNBXKVEYMXNXLXTYKYMXNXLXTYKVAZXNXLUHYMXEXIOZ
        UFZWTYQPZUHZYPXLXNYMYTVAXLXNYMYTXLYTXNYMUHXLYRXNYSYMXLYQXKXEXKXIVFZVGXL
        YQXKWTUUAVHVIVJVKVLYTYIXTXHYIYTXTXHVAZGVMZYIYTUUBABCDEFGHIJKLVNUUCYIYTU
        IZXHXTUUDXDXQXGXSUUCYIYSXDXQUBYRWTGXIVOVPUUDXFXRHUUCYIYRXFXRUBYSXEGXIVQ
        VRVSVTVJWAWBWCWDVKWEWFWGWHYAYMXTXTBXKVEVLWIWJWKWEWLWMWNWOWPWQWRWSWRWS
        $.
        $( [21-Apr-2011] $)
    $}
  $}

  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y $.  $d R f x y z $.
    $d C f x y $.
    wfrlem13.1 $e |- R We A $.
    wfrlem13.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfrlem13.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                           ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfrlem13.4 $e |- F = U. B $.
    wfrlem13.5 $e |- C = ( F u.
                      { <. z , ( G ` ( F |` Pred ( R , A , z ) ) ) >. } ) $.
    $( Lemma for well-founded recursion.  From here through ~ wfrlem16 , we aim
       to prove that ` dom F = A ` .  We do this by supposing that there is an
       element ` z ` of ` A ` that is not in ` dom F ` .  We then define ` C `
       by extending ` dom F ` with the appropriate value at ` z ` .  We then
       show that ` z ` cannot be an ` R ` minimal element of
       ` ( A \ dom F ) ` , meaning that ` ( A \ dom F ) ` must be empty, so
       ` dom F = A ` .  Here, we show that ` C ` is a function extending the
       domain of ` F ` by one. $)
    wfrlem13 $p |- ( z e. ( A \ dom F ) -> C Fn ( dom F u. { z } ) ) $=
      ( cv cdm cdif wcel cpred cres cfv cop csn cun wfun wceq wa wfn cin c0
      funun wfrlem11 visset fvex funsn pm3.2i wn eldifn disjsn sylibr dmsnop
      ineq2i syl5eq sylancr dmun uneq2i eqtri jctir fneq1i df-fn bitri ) CPZDIQ
      ZRSZIVMIDGVMTUAZJUBZUCUDZUEZUFZVSQZVNVMUDZUEZUGZUHZFWCUIZVOVTWDIUFZVRUFZU
      HVNVRQZUJZUKUGVTVOIVRULWGWHABDEGHIJKLMNUMVMVQCUNVPJUOUPUQVOVNWBUJZUKWJVOV
      MVNSURWKUKUGVMDVNUSVNVMUTVAWIWBVNVMVQVBZVCVDVEWAVNWIUEWCIVRVFWIWBVNWLVGVH
      VIWFVSWCUIWEWCFVSOVJVSWCVKVLVA $.
      $( [21-Apr-2011] $)


    $( Lemma for well-founded recursion.  Compute the value of ` C ` . $)
    wfrlem14 $p |- ( z e. ( A \ dom F ) ->
                     ( y e. ( dom F u. { z } ) ->
                       ( C ` y ) = ( G ` ( C |` Pred ( R , A , y ) ) ) ) ) $=
      ( cv cdm cdif wcel csn cun wfn cfv cpred cres wceq wi wfrlem13 weq wo wa
      wfun wb wss cop ssun1 sseqtr4i w3a funssfv fun2ssres wfrlem9 syl3an3
      fveq2d eqeq12d mp3an2 fnfun sylan wfrlem12 syl5bir ex pm2.43d fveq2
      predeq3 reseq2 syl ssun2 visset snid sselii c0 reseq1 ax-mp resundir
      eqtri wn wfr wwe wefr predfrirr fvex ressn0 uneq2i un0 3eqtri fveq2i
      opeq2i opex elsnc mpbir elun2 eleqtrri fnopfvb mpbiri mpan2 syl5cbir
      jaod elun elsn orbi2i bitri syl5ib ) CPZDIQZRSFXMXLTZUAZUBZBPZXOSZXQFUCZF
      DGXQUDZUEZJUCZUFZUGABCDEFGHIJKLMNOUHXPXQXMSZBCUIZUJZYCXRXPYDYCYEXPYDYCXPY
      DYDYCUGXPYDUKYCXQIUCZIXTUEZJUCZUFZYDFULZYDYCYJUMZXPYKIFUNZYDYLIIXLIDGXLUD
      ZUEZJUCZUOZTZUAZFIYRUPOUQYKYMYDURZXSYGYBYIXQFIUSYTYAYHJYKYMXTXMUNYAYHUFYD
      XTFIUTABDEGHIJXQMNVAVBVCVDVEXOFVFVGABDEGHIJKLMNVHVIVJVKYEYCXLFUCZFYNUEZJU
      CZUFZXPYEXSUUAYBUUCXQXLFVLYEYAUUBJYEXTYNUFYAUUBUFDGXQXLVMXTYNFVNVOVCVDXPX
      LXOSZUUDXNXOXLXNXMVPXLCVQZVRVSXPUUEUKUUDXLUUCUOZFSUUGYSFUUGYRSZUUGYSSUUHU
      UGYQUFUUCYPXLUUBYOJUUBYOYRYNUEZUAZYOVTUAYOUUBYSYNUEZUUJFYSUFUUBUUKUFOFYSY
      NWAWBIYRYNWCWDUUIVTYOXLYNSWEZUUIVTUFDGWFZUULDGWGUUMKDGWHWBDGXLWIWBYNXLYPU
      UFYOJWJWKWBWLYOWMWNWOWPUUGYQXLUUCWQWRWSUUGYRIWTWBOXAXOXLUUCFUUBJWJXBXCXDX
      EXFXRYDXQXNSZUJYFXQXMXNXGUUNYEYDBXLXHXIXJXKVO $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  When ` z ` is ` R ` minimal, ` C ` is
       an acceptable function. $)
    wfrlem15 $p |- ( ( z e. ( A \ dom F ) /\
                       Pred ( R , ( A \ dom F ) , z ) = (/) ) ->
                     C e. B ) $=
      ( cv cdm cdif wcel cpred c0 wceq wa wfn wss wral cfv cres w3a wex csn
      cun cvv unexg wfrlem10 setlikespec eldifi sylancl adantr eqeltrrd snex
      wfrlem13 unss biimpi wfrlem7 snssd sylancr weq wo wi wfrlem9 ssun3 syl
      a1i predeq3 sseq1d ssun1 mpbiri syl5cbir jaod elun elsn orbi2i bitri
      syl5ib r19.21aiv jca wfrlem14 3jca fneq2 sseq1 sseq2 raleqbi1dv anbi12d
      raleq 3anbi123d cla4egv sylc wb fnex mpan2 sylan2 syl11anc fneq1 fveq1
      reseq1 fveq2d eqeq12d ralbidv 3anbi13d exbidv elab2g mpbird ) CPZDIQZRZSZ
      XPGXNTUAUBZUCZFESZFAPZUDZYADUEZDGBPZTZYAUEZBYAUFZUCZYDFUGZFYEUHZJUGZUBZBY
      AUFZUIZAUJZXSXOXNUKZULZUMSZFYQUDZYQDUEZYEYQUEZBYQUFZUCZYLBYQUFZUIZYOXOUMS
      ZYPUMSZYRXSXOYPUMUMUNZXSDGXNTZXOUMABCDEGHIJKMNUOZXQUUIUMSZXRXNDSDGYATUMSA
      DUFUUKXQADGXNUPXNDXOUQZLURUSUTZXNVAZURXSYSUUCUUDXQYSXRABCDEFGHIJKLMNOVBUS
      ZXSYTUUBXQYTXRXODUEZYPDUEZYTXQUUPUUQUCYTXOYPDVCVDABDEGHIJMNVEXQXNDUULVFVG
      USXSUUABYQXSYDXOSZBCVHZVIZUUAYDYQSZXSUURUUAUUSUURUUAVJXSUURYEXOUEUUAABDEG
      HIJYDMNVKYEXOYPVLVMVNUUSUUAUUIYQUEZXSUUSYEUUIYQDGYDXNVOVPXSUVBXOYQUEXOYPV
      QXSUUIXOYQUUJVPVRVSVTUVAUURYDYPSZVIUUTYDXOYPWAUVCUUSUURBXNWBWCWDWEWFWGXQU
      UDXRXQYLBYQABCDEFGHIJKLMNOWHWFUSWIYNUUEAYQUMYAYQUBZYBYSYHUUCYMUUDYAYQFWJU
      VDYCYTYGUUBYAYQDWKYFUUABYAYQYAYQYEWLWMWNYLBYAYQWOWPWQWRXSFUMSZXTYOWSXSYSU
      UFUVEUUOUUMYSYRUVEUUFYQUMFWTUUFUUGYRUUNUUHXAXBXCHPZYAUDZYHYDUVFUGZUVFYEUH
      ZJUGZUBZBYAUFZUIZAUJYOHFEUMUVFFUBZUVMYNAUVNUVGYBUVLYMYHYAUVFFXDUVNUVKYLBY
      AUVNUVHYIUVJYKYDUVFFXEUVNUVIYJJUVFFYEXFXGXHXIXJXKMXLVMXM $.
      $( [21-Apr-2011] $)

    $( Lemma for well-founded recursion.  If ` z ` is ` R ` minimal in
       ` ( A \ dom F ) ` , then ` C ` is acceptable and thus a subset of
       ` F ` , but ` dom C ` is bigger than ` dom F ` .  Thus, ` z ` cannot be
       minimal, so ` ( A \ dom F ) ` must be empty, and (due to ~ wfrlem7 ),
       ` dom F = A ` . $)
    wfrlem16 $p |- dom F = A $=
      ( cdm wfrlem7 wss cdif c0 wceq cv cpred wrex wcel eldifn wa csn cun
      ssun2 visset snid sselii cres cfv cop dmeqi dmun dmsnop uneq2i 3eqtri
      eleqtrri cuni wfrlem15 elssuni syl syl6ssr dmss sseld mpi mtand nrex wn
      wne df-ne difss tz6.26i mpan sylbir mt3 ssdif0 mpbir eqssi ) IPZDABDEGHIJ
      MNQDWDRDWDSZTUAZWFWEGCUBZUCTUAZCWEUDZWHCWEWGWEUEZWHWGWDUEZWGDWDUFWJWHUGZW
      GFPZUEWKWGWDWGUHZUIZWMWNWOWGWNWDUJWGCUKULUMWMIWGIDGWGUCUNJUOZUPUHZUIZPWDW
      QPZUIWOFWROUQIWQURWSWNWDWGWPUSUTVAVBWLWMWDWGWLFIRWMWDRWLFEVCZIWLFEUEFWTRA
      BCDEFGHIJKLMNOVDFEVEVFNVGFIVHVFVIVJVKVLWFVMWETVNZWIWETVOWEDRXAWIDWDVPACDW
      EGKLVQVRVSVTDWDWAWBWC $.
      $( [21-Apr-2011] $)

  $}

  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y $.  $d R f x y z $.
    wfr1.1 $e |- R We A $.
    wfr1.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr1.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr1.4 $e |- F = U. B $.
    $( The Principle of Well-Founded Recursion, part 1 of 3.  We start with an
       arbitrary function ` G ` and a class of "acceptable" functions ` B ` .
       Then, using a base class ` A ` and a well-ordering ` R ` of ` A ` , we
       define a function ` F ` .  This function is said to be defined by
       "well-founded recursion."  The purpose of these three theorems is to
       demonstrate the properties of ` F ` .  We begin by showing that ` F ` is
       a function over ` A ` . $)
    wfr1 $p |- F Fn A $=
      ( vz wfn wfun cdm wceq df-fn wfrlem11 cv cpred cres cfv cop csn cun eqid
      wfrlem16 mpbir2an ) GCNGOGPCQGCRABCDEFGHIJKLSABMCDGMTZGCEUJUAUBHUCUDUEUFZ
      EFGHIJKLUKUGUHUI $.
      $( [22-Apr-2011] $)
  $}

  ${
    $d A f w x y $.  $d F f w x y $.  $d G f x y $.  $d R f w x y $.  $d y z $.
    wfr2.1 $e |- R We A $.
    wfr2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr2.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr2.4 $e |- F = U. B $.
    $( The Principle of Well-Founded Recursion, part 2 of 3.  Next, we show
       that the value of ` F ` at any ` z e. A ` is ` G ` recursively applied
       to all "previous" values of ` F ` . $)
    wfr2 $p |- ( z e. A ->
                  ( F ` z ) = ( G ` ( F |` Pred ( R , A , z ) ) ) ) $=
      ( vw cv cfv cpred cres wceq weq fveq2 predeq3 reseq2 syl fveq2d eqeq12d
      wcel cdm cop csn cun eqid wfrlem16 eleq2i wfrlem12 sylbir vtoclga ) BOZHP
      ZHDFURQZRZIPZSZCOZHPZHDFVDQZRZIPZSBVDDBCTZUSVEVBVHURVDHUAVIVAVGIVIUTVFSVA
      VGSDFURVDUBUTVFHUCUDUEUFURDUGURHUHZUGVCVJDURABNDEHNOZHDFVKQRIPUIUJUKZFGHI
      JKLMVLULUMUNABDEFGHIJKLMUOUPUQ $.
      $( [18-Apr-2011] $)

  $}

  ${
    $d A f x y z $.  $d F f x y z $.  $d G f x y z $.  $d H x z $.
    $d R f x y z $.
    wfr3.1 $e |- R We A $.
    wfr3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfr3.3 $e |- B = { f |
                           E. x ( f Fn x /\
                                  ( x C_ A /\
                                    A. y e. x Pred ( R , A , y ) C_ x ) /\
                                  A. y e. x ( f ` y ) =
                                            ( G ` ( f |` Pred ( R , A , y ) ) )
                                ) } $.
    wfr3.4 $e |- F = U. B $.
    $( The principle of Well-Founded Recursion, part 3 of 3.  Finally, we show
       that ` F ` is unique.  We do this by showing that any function ` H `
       with the same properties we proved of ` F ` in ~ wfr1 and ~ wfr2 is
       identical to ` F ` . $)
    wfr3 $p |- ( ( H Fn A /\
                    A. z e. A ( H ` z ) =
                            ( G ` ( H |` Pred ( R , A , z ) ) ) ) ->
                  F = H ) $=
      ( wwe cv cpred cvv wcel wral wa wfn cfv cres wceq pm3.2i wfr1 wfr2 rgen
      wfr3g mp3an12 ) DFOZDFAPQRSADTZUAHDUBZCPZHUCHDFUOQZUDIUCUEZCDTZUAJDUBUOJU
      CJUPUDIUCUECDTUAHJUEULUMKLUFUNURABDEFGHIKLMNUGUQCDABCDEFGHIKLMNUHUIUFACDF
      HJIUJUK $.
      $( [18-Apr-2011] $)
  $}


