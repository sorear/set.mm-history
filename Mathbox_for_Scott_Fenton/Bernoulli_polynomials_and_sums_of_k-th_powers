$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Bernoulli polynomials and sums of k-th powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c BernPoly $.

  $( Declare the constant for the Bernoulli polynomial operator. $)
  cbp $a class BernPoly $.

  ${
    $d e f g k m n s x $.
    $( Define the Bernoulli polynomials.  Here we use well-founded recursion to
       define the Bernoulli polynomials.  This agrees with most textbook
       definitions, although explicit formulae do exist. $)
    df-bpoly $a |- BernPoly =
       ( m e. NN0 , x e. CC |->
         ( U. { f | E. s ( f Fn s /\
                           ( s C_ NN0 /\
                             A. e e. s Pred ( < , NN0 , e ) C_ s ) /\
                             A. e e. s ( f ` e ) =
          ( ( g e. _V |-> [_ ( # ` dom g ) / n ]_
            ( ( x ^ n ) - sum_ k e. dom g ( ( n _C k ) x.
              ( ( g ` k ) / ( ( n - k )
              + 1 ) ) ) ) ) ` ( f |` Pred ( < , NN0 , e ) ) ) ) } ` m ) ) $.
  $}

  ${
    $d e f g k m n s x F $.  $d e g k m n x N $.  $d a b c e f g k m n s x X $.
    $d e f s G $.
    ${
      bpoly.1 $e |- G = ( g e. _V |-> [_ ( # ` dom g ) / n ]_
         ( ( X ^ n ) - sum_ k e. dom g ( ( n _C k ) x.
           ( ( g ` k ) / ( ( n - k ) + 1 ) ) ) ) ) $.
      bpoly.2 $e |- F = U. { f | E. s ( f Fn s /\ ( s C_ NN0 /\
        A. e e. s Pred ( < , NN0 , e ) C_ s ) /\ A. e e. s ( f ` e ) =
          ( G ` ( f |` Pred ( < , NN0 , e ) ) ) ) } $.
      $( Lemma for ~ bpolyval . $)
      bpolylem $p |- ( ( N e. NN0 /\ X e. CC ) ->
         ( N BernPoly X ) = ( ( X ^ N ) -
            sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
              ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) $=
        ( cn0 wcel co cfv clt cmin cvv wceq vm vx cc wa cbp cpred cres cexp cc0
        c1 cfz cv cbc caddc cdiv cmul csu wfn wss wral cdm csb cmpt w3a wex cab
        chash cuni simpl opreq1d csbeq2dv mpan2 mpteq2dva syl6eqr fveq1d eqeq2d
        fvex ralbidv 3anbi3d exbidv abbidv fveq2 sylan9eqr df-bpoly ovmpt2a wwe
        unieqd cuz ltfrn nn0uz weeq2 ax-mp mpbir nn0ex predasetex a1i rgen eqid
        wb wfr2c adantr prednn0 reseq2d fveq2d wfun fnfun oprex resfunexg mp2an
        wfr1 dmeq elfznn0 ssriv fnssres fndm syl6eq sumeq1d fveq1 fvres opreq2d
        sylan9eq sumeq2dv eqtrd csbeq1d csbex fvmpt a17d opreq2 opreq1 opreq12d
        sumeq2sdv csbiegf cen wbr cz nn0z fz01en syl cfn fzfi hashen hashfz1 wi
        sylibr idi simpr syl2anr 3eqtr4d syl5eq 3eqtrd ) HMNZIUCNZUDZHIUEOHFPZF
        MQHUFZUGZGPZIHUHOZUIHUJROZUKOZHDULZUMOZUVAIUEOZHUVAROZUJUNOZUOOZUPOZDUQ
        ZROZUAUBHIMUCUAULZBULZJULZURZUVLMUSMQAULZUFZUVLUSAUVLUTUDZUVNUVKPZUVKUV
        OUGZCSECULZVAZVGPZUBULZEULZUHOZUVTUWCUVAUMOZUVAUVSPZUWCUVAROZUJUNOZUOOZ
        UPOZDUQZROZVBZVCZPZTZAUVLUTZVDZJVEZBVFZVHZPZUUNUEUWBITZUVJHTUXBUVJFPZUU
        NUXCUVJUXAFUXCUXAUVMUVPUVQUVRGPZTZAUVLUTZVDZJVEZBVFZVHFUXCUWTUXJUXCUWSU
        XIBUXCUWRUXHJUXCUWQUXGUVMUVPUXCUWPUXFAUVLUXCUWOUXEUVQUXCUVRUWNGUXCUWNCS
        EUWAIUWCUHOZUWKROZVBZVCGUXCCSUWMUXMUXCUVSSNZUDZUWASNZUWMUXMTUVTVGVQZUXO
        EUWAUWLUXLSUXOUWDUXKUWKRUXOUWBIUWCUHUXCUXNVIVJVJVKVLVMKVNVOVPVRVSVTWAWG
        LVNVOZUVJHFWBWCUBABCDUAEJWDZHFVQWEUUKUUNUUQTUULJAMUXJQBFGHMQWFZUIWHPZQW
        FZUIWIMUYATUXTUYBWSWJMUYAQWKWLWMZMQUVLUFSNZJMUYDUVLMNMQUVLWNWOWPWQZUXJW
        RZLWTXAUUMUUQFUUTUGZGPZUVIUUMUUPUYGGUUMUUOUUTFUUKUUOUUTTUULHXBXAXCXDUUM
        UYHEUUTVGPZUXKUUTUWEUVAFPZUWHUOOZUPOZDUQZROZVBZUVIUYGSNZUYHUYOTFXEZUUTS
        NUYPFMURZUYQJAMUXJQBFGUYCUYEUYFLXJZMFXFWLUIUUSUKXGFUUTSXHXICUYGUXMUYOSG
        UVSUYGTZUXMEUWAUYNVBZUYOUYTUXPUXMVUATUXQUYTEUWAUXLUYNSUYTUWKUYMUXKRUYTU
        WKUUTUWJDUQUYMUYTUVTUUTUWJDUYTUVTUYGVAZUUTUVSUYGXKUYGUUTURZVUBUUTTUYRUU
        TMUSVUCUYSDUUTMUVAUUSXLZXMMUUTFXNXIUUTUYGXOWLXPZXQUYTUUTUWJUYLDUYTUVAUU
        TNZUDZUWIUYKUWEUPVUGUWFUYJUWHUOUYTVUFUWFUVAUYGPUYJUVAUVSUYGXRUVAUUTFXSY
        AVJXTYBYCXTVKVLUYTEUWAUYIUYNUYTUVTUUTVGVUEXDYDYCKEUYIUYNUUTVGVQUXKUYMRX
        GYEYFWLUUMEHUYNVBZUURUUTUVBUYJUVEUOOZUPOZDUQZROZUYOUVIUUKVUHVULTUULEAHU
        YNVULMUUKUVNVULNEYGUWCHTZUXKUURUYMVUKRUWCHIUHYHVUMUUTUYLVUJDVUMUWEUVBUY
        KVUIUPUWCHUVAUMYIVUMUWHUVEUYJUOVUMUWGUVDUJUNUWCHUVARYIVJXTYJYKYJYLXAUUM
        EUYIHUYNUUKUYIHTUULUUKUYIUJHUKOZVGPZHUUKUUTVUNYMYNZUYIVUOTZUUKHYONVUPHY
        PHYQYRUUTYSNVUNYSNVUQVUPWSUIUUSYTUJHYTUUTVUNUUAXIUUDHUUBYCXAYDUUMUVHVUK
        UURRUUMUUTUVGVUJDUUMVUFUDZUVFVUIUVBUPVURUVCUYJUVEUOVUFUVAMNZUULUVCUYJTU
        UMVUFVUSUUCVUDUUEUUKUULUUFUAUBUVAIMUCUXBUYJUEUXCUVJUVATUXBUXDUYJUXRUVJU
        VAFWBWCUXSUVAFVQWEUUGVJXTYBXTUUHUUIYCUUJ $.
        $( [22-May-2014] $)
    $}

    $( The value of the Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.) $)
    bpolyval $p |- ( ( N e. NN0 /\ X e. CC ) ->
       ( N BernPoly X ) = ( ( X ^ N ) -
          sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
            ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) $=
      ( ve vn vb vm vs cv cn0 wss wral cfv co cbc cmin c1 cdiv cmul wceq vf wfn
      vg va vx vc clt cpred cres cvv cdm chash cexp caddc csu cmpt w3a wex cuni
      wa cab csb fvex wcel ax-17 opreq2 opreq1 opreq1d opreq2d sumeq2sdv csbief
      opreq12d fveq2 cbvsumv dmeq fveq1 adantr sumeq12dv csbeq2dv mpan2 syl5eqr
      syl5eq fveq2d csbeq1d eqtrd cbvmptv fneq2 sseq1 predeq3 sseq1d raleqbi1dv
      cbvralv sseq2 syl5bb anbi12d raleq 3anbi123d cbvexv fneq1 reseq2d eqeq12d
      reseq1 ralbidv 3anbi13d exbidv cbvabv unieqi bpolylem ) DUAUCAEUDIZUEIZUB
      ZXJJKZJUGFIZUHZXJKZFXJLZUTZXMXIMZXIXNUIZUFUJCUFIZUKZULMZUMNZYAYBGIZONZYDX
      TMZYBYDPNZQUNNZRNZSNZGUOZPNZUPZMZTZFXJLZUQZUEURZUDVAZUSYMBCHUFUCUJYLEUCIZ
      UKZULMZCEIZUMNZUUAUUCAIZONZUUEYTMZUUCUUEPNZQUNNZRNZSNZAUOZPNZVBZXTYTTZYLE
      YBUUMVBZUUNUUOYLEYBUUDYAUUCYDONZYFUUCYDPNZQUNNZRNZSNZGUOZPNZVBZUUPEDYBUVC
      YLYAULVCZDIZYLVDEVEUUCYBTZUUDYCUVBYKPUUCYBCUMVFUVGYAUVAYJGUVGUUQYEUUTYISU
      UCYBYDOVGUVGUUSYHYFRUVGUURYGQUNUUCYBYDPVGVHVIVLVJVLVKUUOYBUJVDUVDUUPTUVEU
      UOEYBUVCUUMUJUUOUVBUULUUDPUUOUVBYAUUFUUEXTMZUUIRNZSNZAUOUULYAUVAUVJGAYDUU
      ETZUUQUUFUUTUVISYDUUEUUCOVFUVKYFUVHUUSUUIRYDUUEXTVMUVKUURUUHQUNYDUUEUUCPV
      FVHVLVLVNUUOYAUUAUVJUUKAXTYTVOZUUOUVJUUKTUUEYAVDUUOUVIUUJUUFSUUOUVHUUGUUI
      RUUEXTYTVPVHVIVQVRWBVIVSVTWAUUOEYBUUBUUMUUOYAUUAULUVLWCWDWEWFYSUAIZHIZUBZ
      UVNJKZJUGUVFUHZUVNKZDUVNLZUTZUVFUVMMZUVMUVQUIZYMMZTZDUVNLZUQZHURZUAVAYRUW
      GUDUAYRXIUVNUBZUVTYOFUVNLZUQZHURXIUVMTZUWGYQUWJUEHXJUVNTZXKUWHXQUVTYPUWIX
      JUVNXIWGUWLXLUVPXPUVSXJUVNJWHXPUVQXJKZDXJLUWLUVSXOUWMFDXJXMUVFTZXNUVQXJJU
      GXMUVFWIZWJWLUWMUVRDXJUVNXJUVNUVQWMWKWNWOYOFXJUVNWPWQWRUWKUWJUWFHUWKUWHUV
      OUWIUWEUVTUVNXIUVMWSUWIUVFXIMZXIUVQUIZYMMZTZDUVNLUWKUWEYOUWSFDUVNUWNXRUWP
      YNUWRXMUVFXIVMUWNXSUWQYMUWNXNUVQXIUWOWTWCXAWLUWKUWSUWDDUVNUWKUWPUWAUWRUWC
      UVFXIUVMVPUWKUWQUWBYMXIUVMUVQXBWCXAXCWNXDXEWNXFXGXH $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at zero.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpoly0 $p |- ( X e. CC -> ( 0 BernPoly X ) = 1 ) $=
      ( vk cc wcel cc0 cbp co cexp c1 cmin cfz cv cbc wceq c0 ax-mp cz 0z eqtri
      csu caddc cdiv cmul cn0 0nn0 bpolyval mpan exp0 opreq1d clt wbr cr 0re wb
      ltm1 peano2zm mp2an mpbi sumeq1i sum0 opreq2i ax-1cn subid1i syl6eq eqtrd
      fzn ) ACDZEAFGZAEHGZEEIJGZKGZEBLZMGVLAFGEVLJGIUAGUBGUCGZBTZJGZIEUDDVGVHVO
      NUEBEAUFUGVGVOIVNJGZIVGVIIVNJAUHUIVPIEJGIVNEIJVNOVMBTEVKOVMBVJEUJUKZVKONZ
      EULDVQUMEUOPEQDZVJQDZVQVRUNRVSVTREUPPEVJVFUQURUSVMBUTSVAIVBVCSVDVE $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at one.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpoly1 $p |- ( X e. CC -> ( 1 BernPoly X ) = ( X - ( 1 / 2 ) ) ) $=
      ( vk cc wcel c1 cbp co cc0 cmin cfz cbc caddc cdiv cmul csu ax-1cn syl6eq
      c2 wceq opreq12d cexp cv cn0 1nn nnnn0i bpolyval mpan exp1 subidi opreq2i
      sumeq1i cz 0z wi bpoly0 opreq1d opreq2d halfcl ax-mp mulid2i idi syl6eqel
      opreq2 1nn0 bcn0 opreq1 subid1i df-2 syl6eqr fsum1 sylancr eqtrd syl5eq )
      ACDZEAFGZAEUAGZHEEIGZJGZEBUBZKGZVSAFGZEVSIGZELGZMGZNGZBOZIGZAERMGZIGEUCDZ
      VNVOWGSEUDUEBEAUFUGVNVPAWFWHIAUHVNWFHHJGZWEBOZWHVRWJWEBVQHHJEPUIUJUKVNWKE
      HAFGZRMGZNGZWHVNHULDWNCDWKWNSUMVNWNWHCVNWNWHSUNVNWNEWHNGWHVNWMWHENVNWLERM
      AUOUPUQWHECDWHCDPEURUSZUTQVAZWOVBWEWNBHVSHSZVTEWDWMNWQVTEHKGZEVSHEKVCWIWR
      ESVDEVEUSQWQWAWLWCRMVSHAFVFWQWCEELGRWQWBEELWQWBEHIGEVSHEIVCEPVGQUPVHVITTV
      JVKWPVLVMTVL $.
      $( [16-May-2014] $)
  $}

  ${
    $d N n $.  $d X k m n $.
    $( Closure law for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.)  (The proof was shortened by Mario Carneiro,
       22-May-2014.) $)
    bpolycl $p |- ( ( N e. NN0 /\ X e. CC ) -> ( N BernPoly X ) e. CC ) $=
      ( vk vm cn0 wcel cc cbp co cv wceq opreq1 eleq1d cc0 cmin cfz syl2anc syl
      wi c1 vn imbi2d wral r19.21v w3a cexp cbc caddc cdiv csu bpolyval 3adant3
      cmul simp2 simp1 expcl fzfid wa cz elfzelz bccl syl2an nn0cn wne rcla4cva
      simp3 sylan cn wss cvv oprex fzssp1 ax-mp ax-1cn sylancl opreq2d syl5sseq
      npcan ssel2 fznn0sub syldan nn0p1nn nncn nnne0 divcl syl3anc mulcl fsumcl
      subcl eqeltrd 3exp a2d syl5bi nn0sinds imp ) AEFBGFZABHIZGFZWPUAJZBHIZGFZ
      SZWPCJZBHIZGFZSZWPWRSUACAWSXCKZXAXEWPXGWTXDGWSXCBHLMUBWSAKZXAWRWPXHWTWQGW
      SABHLMUBXFCNWSTOIZPIZUCWPXECXJUCZSWSEFZXBWPXECXJUDXLWPXKXAXLWPXKXAXLWPXKU
      EZWTBWSUFIZXJWSDJZUGIZXOBHIZWSXOOIZTUHIZUIIZUMIZDUJZOIZGXLWPWTYCKXKDWSBUK
      ULXMXNGFZYBGFYCGFXMWPXLYDXLWPXKUNXLWPXKUOZBWSUPQXMXJYADXMNXIUQXMXOXJFZURZ
      XPGFZXTGFZYAGFYGXPEFZYHXMXLXOUSFYJYFYEXONXIUTXOWSVAVBXPVCRYGXQGFZXSGFZXSN
      VDZYIXMXKYFYKXLWPXKVFXEYKCXOXJXCXOKXDXQGXCXOBHLMVEVGYGXSVHFZYLYGXREFZYNXM
      YFXONWSPIZFZYOXMXJYPVIYFYQXMNXITUHIZPIZXJYPXIVJFXJYSVIWSTOVKNXIVJVLVMXMYR
      WSNPXMWSGFZTGFYRWSKXMXLYTYEWSVCRVNWSTVRVOVPVQXJYPXOVSVGXMXLYQYOYEEXONWSVT
      VGWAXRWBRZXSWCRYGYNYMUUAXSWDRXQXSWEWFXPXTWGQWHXNYBWIQWJWKWLWMWNWO $.
      $( [22-May-2014] $) $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.
    $( A sum for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.)  (The proof was shortened by Mario Carneiro,
       22-May-2014.) $)
    bpolysum $p |- ( ( N e. NN0 /\ X e. CC ) ->
      sum_ k e. ( 0 ... N ) ( ( N _C k ) x. ( ( k BernPoly X ) / ( ( N - k ) +
        1 ) ) ) = ( X ^ N ) ) $=
      ( cn0 wcel cc cc0 cfz co cbc cbp cmin c1 caddc cdiv cmul syl wceq opreq2d
      opreq12d wa cv csu cexp cuz cfv simpl nn0uz syl6eleq elfzelz syl2an nn0cn
      cz bccl elfznn0 simpr bpolycl syl2anr fznn0sub adantlr nn0p1nn nncn nnne0
      wne divcl syl3anc mulcl syl2anc opreq2 opreq1 opreq1d fsumm1 adantr subid
      cn bcnn ax-1cn addid2i syl6eq div1 eqtrd mulid2 bpolyval eqcomd wb ancoms
      expcl fzfid wss cvv oprex fzssp1 ax-mp npcan sylancl syl5sseq ssel2 sylan
      syldan fsumcl subadd mpbid 3eqtrd ) BDEZCFEZUAZGBHIZBAUBZJIZXHCKIZBXHLIZM
      NIZOIZPIZAUCGBMLIZHIZXNAUCZBBJIZBCKIZBBLIZMNIZOIZPIZNIXQXSNIZCBUDIZXFXNYC
      AGBXFBDGUEUFXDXEUGZUHUIXFXHXGEZUAZXIFEZXMFEZXNFEZYHXIDEZYIXFXDXHUMEYLYGYF
      XHGBUJXHBUNUKXIULQYHXJFEZXLFEZXLGVDZYJYGXHDEXEYMXFXHBUOXDXEUPXHCUQURYHXLV
      OEZYNYHXKDEZYPXDYGYQXEDXHGBUSUTXKVAQZXLVBQYHYPYOYRXLVCQXJXLVEVFXIXMVGVHZX
      HBRZXIXRXMYBPXHBBJVIYTXJXSXLYAOXHBCKVJYTXKXTMNXHBBLVIVKTTVLXFYCXSXQNXFYCM
      XSPIZXSXFXRMYBXSPXDXRMRXEBVPVMXFYBXSMOIZXSXFYAMXSOXFYAGMNIMXFXTGMNXFBFEZX
      TGRXDUUCXEBULVMZBVNQVKMVQVRVSSXFXSFEZUUBXSRBCUQZXSVTQWATXFUUEUUAXSRUUFXSW
      BQWASXFYEXQLIZXSRZYDYERZXFXSUUGABCWCWDXFYEFEZXQFEUUEUUHUUIWEXEXDUUJCBWGWF
      XFXPXNAXFGXOWHXFXHXPEZYGYKXFXPXGWIUUKYGXFGXOMNIZHIZXPXGXOWJEXPUUMWIBMLWKG
      XOWJWLWMXFUULBGHXFUUCMFEUULBRUUDVQBMWNWOSWPXPXGXHWQWRYSWSWTUUFYEXQXSXAVFX
      BXC $.
      $( [22-May-2014] $) $( [16-May-2014] $)
  $}

  ${
    $d k m n N $.  $d k m ph $.  $d k m n X $.
    ${
      bpolydiflem.1 $e |- ( ph -> N e. NN ) $.
      bpolydiflem.2 $e |- ( ph -> X e. CC ) $.
      bpolydiflem.3 $e |- ( ( ph /\ k e. ( 1 ... ( N - 1 ) ) ) ->
                            ( ( k BernPoly ( X + 1 ) ) - ( k BernPoly X ) ) =
                              ( k x. ( X ^ ( k - 1 ) ) ) ) $.
      $( Lemma for ~ bpolydif . $)
      bpolydiflem $p |- ( ph ->
                          ( ( N BernPoly ( X + 1 ) ) - ( N BernPoly X ) ) =
                            ( N x. ( X ^ ( N - 1 ) ) ) ) $=
        ( c1 caddc co cmin cc0 cfz cdiv cmul wcel cc wceq syl syl2anc vm cbp cv
        cexp cbc csu cn nnnn0 peano2cn bpolyval opreq12d expcl fzfid wa elfzelz
        cn0 cz bccl syl2an nn0cn wne elfznn0 bpolycl syl2anr oprex fzssp1 ax-mp
        wss cvv nncn ax-1cn npcan sylancl opreq2d syl5sseq ssel2 sylan fznn0sub
        syldan nn0p1nn nnne0 divcl syl3anc mulcl fsumcl sub4 syl22anc c2 addcom
        opreq1d binom1p eqtrd cuz cfv nn0uz syl6eleq bccl2 opreq2 fsumm1 mulid2
        bcnn 3eqtrd pncan nnm1nn0 a1i subsub4 df-2 opreq2i syl6eqr bcnm1 opreq1
        sumeq1d fsum1p bpoly0 0z subid1 eqeltrd peano2nn nnrecre fzp1ss sylancr
        cr recnd pnpcan 1z nnz 2z zsubcl wi idi 2cn subsub eqcomd opreq1i mp2an
        eqtri adantr syl112anc 3eqtr3d mulass syl6eq eleq2i bcm1k elfznn adantl
        fsumshft addid2i sylan2br divsubdir div23 subdi divcan6 eqtr3d sumeq2dv
        nn0z sylan2b fsumsub 3eqtr2rd pncan2 ) ACDHIJZUBJZCDUBJZKJUUTCUDJZLCHKJ
        ZMJZCBUCZUEJZUVFUUTUBJZCUVFKJZHIJZNJZOJZBUFZKJZDCUDJZUVEUVGUVFDUBJZUVJN
        JZOJZBUFZKJZKJZUVCUVOKJZUVMUVSKJZKJZCDUVDUDJZOJZAUVAUVNUVBUVTKACUPPZUUT
        QPZUVAUVNRACUGPZUWGECUHZSZADQPZUWHFDUISZBCUUTUJTAUWGUWLUVBUVTRUWKFBCDUJ
        TUKAUVCQPZUVMQPUVOQPZUVSQPUWAUWDRAUWHUWGUWNUWMUWKUUTCULTAUVEUVLBALUVDUM
        ZAUVFUVEPZUNZUVGQPZUVKQPZUVLQPZUWRUVGUPPZUWSAUWGUVFUQPUXBUWQUWKUVFLUVDU
        OUVFCURUSUVGUTSZUWRUVHQPZUVJQPZUVJLVAZUWTUWQUVFUPPZUWHUXDAUVFUVDVBZUWMU
        VFUUTVCVDZUWRUVJUGPZUXEUWRUVIUPPZUXJAUWQUVFLCMJZPZUXKAUVEUXLVHZUWQUXMAL
        UVDHIJZMJZUVEUXLUVDVIPZUVEUXPVHCHKVEZLUVDVIVFVGAUXOCLMACQPZHQPZUXOCRAUW
        IUXSECVJZSZVKCHVLVMZVNVOZUVEUXLUVFVPVQAUWIUXMUXKEUGUVFLCVRVQVSUVIVTSZUV
        JVJSZUWRUXJUXFUYEUVJWASZUVHUVJWBZWCUVGUVKWDTZWEAUWLUWGUWOFUWKDCULTZAUVE
        UVRBUWPUWRUWSUVQQPZUVRQPZUXCUWRUVPQPZUXEUXFUYKUWQUXGUWLUYMAUXHFUVFDVCVD
        ZUYFUYGUVPUVJWBZWCUVGUVQWDTZWEUVCUVMUVOUVSWFWGAUWDLCWHKJZMJZCUAUCZUEJZD
        UYSUDJZOJZUAUFZUWFIJZVUCKJZUWFAUWBVUDUWCVUCKAUWBUVEVUBUAUFZLUVDHKJZMJZV
        UBUAUFZCUVDUEJZUWEOJZIJVUDAUWBVUFUVOIJZUVOKJZVUFAUVCVULUVOKAUVCUXLVUBUA
        UFZVUFCCUEJZUVOOJZIJVULAUVCHDIJZCUDJZVUNAUUTVUQCUDAUWLUXTUUTVUQRFVKDHWI
        VMWJAUWLUWGVURVUNRFUWKDUACWKTWLAVUBVUPUALCACUPLWMWNZUWKWOWPAUYSUXLPZUNZ
        UYTQPZVUAQPZVUBQPZVVAUYTUGPZVVBAUWIVUTVVEEUYSCUGWQVQUYTVJSAUWLUYSUPPVVC
        VUTFUYSCVBDUYSULUSUYTVUAWDTZUYSCRUYTVUOVUAUVOOUYSCCUEWRUYSCDUDWRUKWSAVU
        PUVOVUFIAVUPHUVOOJZUVOAVUOHUVOOAUWGVUOHRUWKCXASWJAUWOVVGUVORUYJUVOWTSWL
        VNXBWJAVUFQPUWOVUMVUFRAUVEVUBUAUWPAUYSUVEPZVUTVVDAUXNVVHVUTUYDUVEUXLUYS
        VPVQVVFVSZWEUYJVUFUVOXCTWLAVUBVUKUALUVDAUVDUPVUSAUWIUVDUPPZECXDSZWOWPZV
        VIUYSUVDRUYTVUJVUAUWEOUYSUVDCUEWRUYSUVDDUDWRUKWSAVUIVUCVUKUWFIAVUHUYRVU
        BUAAVUGUYQLMAVUGCHHIJZKJZUYQAUXSUXTUXTVUGVVNRUYBUXTAVKXEZVVOCHHXFWCWHVV
        MCKXGXHXIVNXLAVUJCUWEOAUWGVUJCRUWKCXJSWJUKXBAUWCCLUEJZHCLKJZHIJZNJZOJZL
        HIJZUVDMJZUVLBUFZIJZVVTVWBUVRBUFZIJZKJZVWCVWEKJZVUCAUVMVWDUVSVWFKAUVMVV
        PLUUTUBJZVVRNJZOJZVWCIJVWDAUVLVWKBLUVDVVLUYIUVFLRZUVGVVPUVKVWJOUVFLCUEW
        RZVWLUVHVWIUVJVVRNUVFLUUTUBXKVWLUVIVVQHIUVFLCKWRWJZUKUKXMAVWKVVTVWCIAVW
        JVVSVVPOAVWIHVVRNAUWHVWIHRUWMUUTXNSWJVNWJWLAUVSVVPLDUBJZVVRNJZOJZVWEIJV
        WFAUVRVWQBLUVDVVLUYPVWLUVGVVPUVQVWPOVWMVWLUVPVWOUVJVVRNUVFLDUBXKVWNUKUK
        XMAVWQVVTVWEIAVWPVVSVVPOAVWOHVVRNAUWLVWOHRFDXNSWJVNWJWLUKAVVTQPZVWCQPVW
        EQPVWGVWHRAVVPQPZVVSQPVWRAVVPUPPZVWSAUWGLUQPZVWTUWKXOLCURVMVVPUTSAVVSAV
        VRUGPZVVSYBPAVVQUGPVXBAVVQCUGAUXSVVQCRUYBCXPSEXQVVQXRSVVRXSSYCVVPVVSWDT
        AVWBUVLBAVWAUVDUMZAUVFVWBPZUWQUXAAVWBUVEVHZVXDUWQAVXAVVJVXEXOVVKLUVDUPX
        TYAVWBUVEUVFVPVQZUYIVSZWEAVWBUVRBVXCAVXDUWQUYLVXFUYPVSZWEVVTVWCVWEYDWCA
        VUCVWBCUVFHKJZUEJZDVXIUDJZOJZBUFZVWBUVLUVRKJZBUFVWHAVUCVWAUYQHIJZMJZVXL
        BUFVXMAVUBVXLUABHLUYQHUQPAYEXEVXAAXOXEACUQPZWHUQPUYQUQPAUWIVXQECYFSYGCW
        HYHVMAUYSUYRPZVVHVVDAUYRUVEVHVXRVVHALVXOMJZUYRUVEUYQVIPUYRVXSVHCWHKVELU
        YQVIVFVGAVXOUVDLMAVXOCWHHKJZKJZUVDAVYAVXOAUXSWHQPZUXTVYAVXORAUXSYIUYBYJ
        VYBAYKXEVVOCWHHYLWCYMVXTHCKVXTVVMHKJZHWHVVMHKXGYNUXTUXTVYCHRVKVKHHXCYOY
        PXHUUAZVNVOUYRUVEUYSVPVQVVIVSZUYSVXIRUYTVXJVUAVXKOUYSVXICUEWRUYSVXIDUDW
        RUKUUFAVXPVWBVXLBAVXOUVDVWAMVYDVNXLWLAVWBVXNVXLBVXDAUVFHUVDMJZPZVXNVXLR
        VWBVYFUVFVWAHUVDMHVKUUGYNUUBZAVYGUNZUVGUVKUVQKJZOJZVXJUVJUVFNJZOJZUVFUV
        JNJZVXKOJZOJZVXNVXLVYIUVGVYMVYJVYOOVYIUVGVXJCVXIKJZUVFNJZOJZVYMVYIUWIUV
        FHCMJZPZUVGVYSRAUWIVYGEYQZAVYFVYTVHVYGWUAAHUXOMJZVYFVYTUXQVYFWUCVHUXRHU
        VDVIVFVGAUXOCHMUYCVNVOVYFVYTUVFVPVQUVFCUGUUCTVYIVYRVYLVXJOVYIVYQUVJUVFN
        VYIUXSUVFQPZUXTVYQUVJRVYIUWIUXSWUBUYASVYIUVFUGPZWUDVYGWUEAUVFUVDUUDUUEZ
        UVFVJSZUXTVYIVKXECUVFHYLWCWJVNWLVYIUVHUVPKJZUVJNJZUVFVXKOJZUVJNJZVYJVYO
        VYIWUHWUJUVJNGWJVYIUXDUYMUXEUXFWUIVYJRAVYGUWQUXDVYGAVXDUWQVYHVXFUUHZUXI
        VSZAVYGUWQUYMWULUYNVSZAVYGUWQUXEWULUYFVSZAVYGUWQUXFWULUYGVSZUVHUVPUVJUU
        IYRVYIWUDVXKQPZUXEUXFWUKVYORWUGVYIUWLVXIUPPZWUQAUWLVYGFYQVYIWUEWURWUFUV
        FXDSZDVXIULTZWUOWUPUVFVXKUVJUUJYRYSUKVYIUWSUWTUYKVYKVXNRAVYGUWQUWSWULUX
        CVSVYIUXDUXEUXFUWTWUMWUOWUPUYHWCVYIUYMUXEUXFUYKWUNWUOWUPUYOWCUVGUVKUVQU
        UKWCVYIVYPVXJVYLVYOOJZOJZVXLVYIVXJQPZVYLQPZVYOQPZVYPWVBRVYIVXJUPPZWVCVY
        IUWGVXIUQPZWVFVYIUWIUWGWUBUWJSVYIWURWVGWUSVXIUUOSVXICURTVXJUTSVYIUXEWUD
        UVFLVAZWVDWUOWUGVYIWUEWVHWUFUVFWASZUVJUVFWBWCZVYIVYNQPZWUQWVEVYIWUDUXEU
        XFWVKWUGWUOWUPUVFUVJWBWCZWUTVYNVXKWDTVXJVYLVYOYTWCVYIWVAVXKVXJOVYIVYLVY
        NOJZVXKOJZWVAVXKVYIWVDWVKWUQWVNWVARWVJWVLWUTVYLVYNVXKYTWCVYIWVNHVXKOJZV
        XKVYIWVMHVXKOVYIUXEUXFWUDWVHWVMHRWUOWUPWUGWVIUVJUVFUULWGWJVYIWUQWVOVXKR
        WUTVXKWTSWLUUMVNWLYSUUPUUNAVWBUVLUVRBVXCVXGVXHUUQUURXBUKAVUCQPUWFQPZVUE
        UWFRAUYRVUBUAALUYQUMVYEWEAUXSUWEQPZWVPUYBAUWLVVJWVQFVVKDUVDULTCUWEWDTVU
        CUWFUUSTWLXB $.
        $( [12-Jun-2014] $)
    $}

    $( Calculate the difference between successive values of the Bernoulli
       polynomials.  (Contributed by Scott Fenton, 16-May-2014.)  (The proof
       was shortened by Mario Carneiro, 26-May-2014.) $)
    bpolydif $p |- ( ( N e. NN /\ X e. CC ) ->
       ( ( N BernPoly ( X + 1 ) ) - ( N BernPoly X ) ) =
       ( N x. ( X ^ ( N - 1 ) ) ) ) $=
      ( vn vk wcel c1 co cbp cmin cexp cmul wceq cv wi opreq12d opreq2d eqeq12d
      opreq1 id imbi2d vm cn cc caddc cfz w3a simp1 simp3 adantr simp2 rcla4cva
      wral wa sylan mpd bpolydiflem 3exp nnsinds imp ) AUBEBUCEZABFUDGZHGZABHGZ
      IGZABAFIGZJGZKGZLZUTCMZVAHGZVIBHGZIGZVIBVIFIGZJGZKGZLZNUTDMZVAHGZVQBHGZIG
      ZVQBVQFIGZJGZKGZLZNZUTVHNCDAVIVQLZVPWDUTWFVLVTVOWCWFVJVRVKVSIVIVQVAHRVIVQ
      BHROWFVIVQVNWBKWFSWFVMWABJVIVQFIRPOQTVIALZVPVHUTWGVLVDVOVGWGVJVBVKVCIVIAV
      AHRVIABHROWGVIAVNVFKWGSWGVMVEBJVIAFIRPOQTVIUBEZWEDFVMUEGZULZUTVPWHWJUTUFZ
      UAVIBWHWJUTUGWHWJUTUHZWKUAMZWIEZUMUTWMVAHGZWMBHGZIGZWMBWMFIGZJGZKGZLZWKUT
      WNWLUIWKWJWNUTXANZWHWJUTUJWEXBDWMWIVQWMLZWDXAUTXCVTWQWCWTXCVRWOVSWPIVQWMV
      AHRVQWMBHROXCVQWMWBWSKXCSXCWAWRBJVQWMFIRPOQTUKUNUOUPUQURUS $.
      $( [16-May-2014] $)
  $}

  ${
    $d K k n $.  $d M k n $.
    $( A closed-form expression for the sum of ` K ` -th powers.  (Contributed
       by Scott Fenton, 16-May-2014.)  (Revised by Mario Carneiro,
       26-May-2014.) $)
    fsumkthpow $p |- ( ( K e. NN0 /\ M e. NN ) ->
       sum_ n e. ( 0 ... M ) ( n ^ K ) =
       ( ( ( ( K + 1 ) BernPoly ( M + 1 ) ) - ( ( K + 1 ) BernPoly 0 ) ) /
         ( K + 1 ) ) ) $=
      ( cn0 wcel wa c1 co cc0 cmul cbp cmin wceq syl cneg bpolycl syl2an opreq2
      cc negeqd vk cn caddc cfz cv cexp csu cdiv wne fzfid cz elfzelz zcn simpl
      expcl syl2anr fsumcl nn0p1nn adantr nncn nnne0 divcan3 fsummulc2 peano2cn
      syl3anc peano2nn0 neg2sub syl2anc bpolydif nn0cn ad2antrr sylancl opreq2d
      ax-1cn pncan 3eqtrd sumeq2dv cuz cfv nnnn0 adantl nn0uz elfznn0 fsumtscop
      syl6eleq negcl eqtr3d 0cn opreq1d ) BDEZCUBEZFZBGUCHZICUDHZAUEZBUFHZAUGZJ
      HZWMUHHZWQWMCGUCHZKHZWMIKHZLHZWMUHHWLWQSEWMSEZWMIUIZWSWQMWLWNWPAWLICUJZWO
      WNEZWOSEZWJWPSEWLXGWOUKEXHWOICULWOUMNZWJWKUNWOBUOUPZUQWLWMUBEZXDWJXKWKBUR
      USZWMUTNZWLXKXEXLWMVANWQWMVBVEWLWRXCWMUHWLWRWNWMWPJHZAUGZXBOZXAOZLHZXCWLW
      NWPWMAXFXMXJVCWLWNWMWOKHZOZWMWOGUCHZKHZOZLHZAUGXOXRWLWNYDXNAWLXGFZYDYBXSL
      HZWMWOWMGLHZUFHZJHZXNYEXSSEZYBSEZYDYFMWLWMDEZXHYJXGWJYLWKBVFZUSZXIWMWOPQW
      LYLYASEZYKXGYNXGXHYOXIWOVDNWMYAPQXSYBVGVHWLXKXHYFYIMXGXLXIWMWOVIQYEYHWPWM
      JYEYGBWOUFYEBSEZGSEYGBMWJYPWKXGBVJVKVNBGVOVLVMVMVPVQWLWMUAUEZKHZOZXTYCXPA
      UAXQICYQWOMYRXSYQWOWMKRTYQYAMYRYBYQYAWMKRTYQIMYRXBYQIWMKRTYQWTMYRXAYQWTWM
      KRTWLCDIVRVSWKCDEWJCVTWAWBWEWLYQIWTUDHEZFZYRSEZYSSEUUAYLYQSEZUUBWLYLYTYNU
      SUUAYQDEZUUCYTUUDWLYQWTWCWAYQVJNWMYQPVHYRWFNWDWGWLXBSEZXASEZXRXCMWLYLISEU
      UEYNWHWMIPVLWJYLWTSEZUUFWKYMWKCSEUUGCUTCVDNWMWTPQXBXAVGVHVPWIWG $.
      $( [26-May-2014] $)
  $}

$( (End of Scott Fenton's mathbox.) $)


