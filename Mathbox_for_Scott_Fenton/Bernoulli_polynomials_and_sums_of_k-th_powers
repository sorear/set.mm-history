$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Bernoulli polynomials and sums of k-th powers
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c BernPoly $.

  $( Declare the constant for the Bernoulli polynomial operator. $)
  cbernpoly $a class BernPoly $.

  ${
    $d m x f s e g k $.
    $( Define the Bernoulli polynomials.  Here we use well-founded recursion to
       define the Bernoulli polynomials.  This agrees with most textbook
       definitions, although explicit formulae do exist. $)
    df-bernpoly $a |- BernPoly =
       ( m e. NN0 , x e. CC |->
         ( U. { f | E. s ( f Fn s /\
                           ( s C_ NN0 /\
                             A. e e. s Pred ( < , NN0 , e ) C_ s ) /\
                             A. e e. s ( f ` e ) =
          ( ( g e. _V |->
              if ( dom g = (/) , 1 ,
              ( ( x ^ ( sup ( dom g , ZZ , < ) + 1 ) ) -
              sum_ k e. dom g ( ( ( sup ( dom g , ZZ , < ) + 1 ) _C k ) x.
              ( ( g ` k ) / ( ( ( sup ( dom g , ZZ , < ) + 1 ) - k )
              + 1 ) ) ) ) ) ) ` ( f |` Pred ( < , NN0 , e ) ) ) ) } ` m ) ) $.
  $}

  ${
    $d e f $.  $d e g $.  $d e h $.  $d e k $.  $d e m $.  $d e n $.  $d e s $.
    $d e t $.  $d e x $.  $d f g $.  $d f h $.  $d f k $.  $d f m $.  $d f n $.
    $d f s $.  $d f t $.  $d f x $.  $d g h $.  $d g k $.  $d g m $.  $d g n $.
    $d g s $.  $d g t $.  $d g x $.  $d h k $.  $d h m $.  $d h s $.  $d h t $.
    $d k m $.  $d k n $.  $d k s $.  $d k t $.  $d k x $.  $d m s $.  $d m t $.
    $d N n $.  $d n s $.  $d n x $.  $d N x $.  $d s t $.  $d s x $.  $d X e $.
    $d X f $.  $d X g $.  $d X h $.  $d X m $.  $d X s $.  $d X t $.  $d X x $.
    $d N h $.  $d N k $.  $d X k $.  $d e j $.  $d f j $.  $d g j $.  $d h j $.
    $d j k $.  $d j m $.  $d j s $.  $d j t $.  $d j x $.  $d j n $.
    $( The value of the Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.) $)
    bernpolyval $p |- ( ( N e. NN0 /\ X e. CC ) ->
       ( N BernPoly X ) =
       if ( N = 0 , 1 ,
            ( ( X ^ N ) -
              sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
                ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) ) $=
      ( vf vs ve vg cn0 wcel co cv clt cfv cvv wceq caddc cmin opreq1d opreq12d
      c1 vn vx vt vm vh vj cc wa cbernpoly wfn wss cpred wral cres c0 csup cexp
      cdm cz cbc cdiv cmul csu cif cmpt w3a wex cab cuni cc0 fveq2 eqidd opreq1
      cfz ifeq2d fveq1d eqeq2d ralbidv 3anbi3d exbidv abbidv unieqd df-bernpoly
      mpteq12dv fvex ovmpt2 wwe ltweuz nn0uz weeq2 ax-mp mpbir nn0ex predasetex
      cuz wb rgen eqid wfrlem1 wfr2c adantr prednn0 reseq2d fveq2d ax-17 opreq2
      a1i weq cbvsumi opreq2i ifeq2 mpteq2i fveq1i eqeq2i ralbii 3anbi3i unieqi
      exbii abbii reseq1 fveq2i wfr1 fnex mp2an resex dmeq dmres elfznn0 eqeq1d
      ssriv supeq1d opreq2d fveq1 sumeq12dv ifbieq2d eqtrd eqtr4d iftrue syl5eq
      cin fndm sseqtr4i df-ss mpbi eqtri syl6eq cbvmptv cr 1re elexi oprex ifex
      fvmpt fz0n ifbid cn wo elnn0 nnm1nn0 syl6eleq supfz syl nncn ax-1cn npcan
      wi sylancl ad2antrr fvres adantl sylan ancoms adantll sumeq2dv jaoi sylbi
      ex a1d imp 3eqtrd ) BHIZCUGIZUHZBCUIJBDKZEKZUJZUWEHUKHLFKZULZUWEUKFUWEUMU
      HZUWGUWDMZUWDUWHUNZGNGKZURZUOOZTCUWMUSLUPZTPJZUQJZUWMUWPAKZUTJZUWRUWLMZUW
      PUWRQJZTPJZVAJZVBJZAVCZQJZVDZVEZMZOZFUWEUMZVFZEVGZDVHZVIZMZUXOHLBULZUNZUX
      HMZBVJOZTCBUQJZVJBTQJZVNJZBUWRUTJZUWRCUIJZBUWRQJZTPJZVAJZVBJZAVCZQJZVDZUA
      UBBCHUGUAKZUWFUWIUWJUWKGNUWNTUBKZUWPUQJZUXEQJZVDZVEZMZOZFUWEUMZVFZEVGZDVH
      ZVIZMUXPUIBVUEMUYMBVUEVKUYNCOZBVUEUXOVUFVUDUXNVUFVUCUXMDVUFVUBUXLEVUFVUAU
      XKUWFUWIVUFUYTUXJFUWEVUFUYSUXIUWJVUFUWKUYRUXHVUFGNUYQNUXGVUFNVLZVUFUWNUYP
      UXFTVUFUYOUWQUXEQUYNCUWPUQVMZRVOWDVPVQVRVSVTWAWBVPUBFDGAUAEWCBUXOWEWFUWAU
      XPUXSOUWBUCUDHUXNLUEUXOUXHBHLWGZVJWOMZLWGZVJWHHVUJOVUIVUKWPWIHVUJLWJWKWLZ
      HLUCKZULNIZUCHVUNVUMHIHLVUMWMWNXGWQZEFUCUDHUXNLDUEUXHUXNWRWSUXOWRWTXAUWCU
      XSUXOUYCUNZUXHMZUYLUWCUXRVUPUXHUWCUXQUYCUXOUWAUXQUYCOUWBBXBXAXCXDUWCVUQUW
      FUWIUWJUWKGNUWNTUWQUWMUWPUFKZUTJZVURUWLMZUWPVURQJZTPJZVAJZVBJZUFVCZQJZVDZ
      VEZMZOZFUWEUMZVFZEVGZDVHZVIZUYCUNZUXHMZUYLVUPVVPUXHUXOVVOOVUPVVPOUXNVVNUX
      MVVMDUXLVVLEUXKVVKUWFUWIUXJVVJFUWEUXIVVIUWJUWKUXHVVHGNUXGVVGUXFVVFOUXGVVG
      OUXEVVEUWQQUBUWMUXDVVDAUFUYNUXDIUFXEUYNVVDIAXEAUFXHZUWSVUSUXCVVCVBUWRVURU
      WPUTXFVVRUWTVUTUXBVVBVAUWRVURUWLVKVVRUXAVVATPUWRVURUWPQXFRSSXIXJUWNUXFVVF
      TXKWKXLXMXNXOXPXRXSXQUXOVVOUYCXTWKYAUWCVVQUYCUOOZTCUYCUSLUPZTPJZUQJZUYCVW
      AUWRUTJZUWRVVPMZVWAUWRQJZTPJZVAJZVBJZAVCZQJZVDZUYLVVPNIVVQVWKOVVOUYCVVOHU
      JZHNIVVONIUCUDHVVNLUEVVOVVHVULVUOEFUCUDHVVNLDUEVVHVVNWRWSVVOWRYBZWMHNVVOY
      CYDYEUEVVPUEKZURZUOOZTCVWOUSLUPZTPJZUQJZVWOVWRUWRUTJZUWRVWNMZVWRUWRQJZTPJ
      ZVAJZVBJZAVCZQJZVDZVWKNUXHVWNVVPOZVWPVVSVXGVWJTVXIVWOUYCUOVXIVWOVVPURZUYC
      VWNVVPYFVXJUYCVVOURZYTZUYCVVOUYCYGUYCVXKUKVXLUYCOUYCHVXKUBUYCHUYNUYBYHYJV
      WLVXKHOVWMHVVOUUAWKUUBUYCVXKUUCUUDUUEUUFZYIVXIVWSVWBVXFVWIQVXIVWRVWACUQVX
      IVWQVVTTPVXIUSVWOUYCLVXMYKRZYLVXIVWOUYCVXEVWHAVXMVXIVXEVWHOUWRVWOIVXIVWTV
      WCVXDVWGVBVXIVWRVWAUWRUTVXNRVXIVXAVWDVXCVWFVAUWRVWNVVPYMVXIVXBVWETPVXIVWR
      VWAUWRQVXNRRSSXAYNSYOGUENUXGVXHGUEXHZUWNVWPUXFVXGTVXOUWMVWOUOUWLVWNYFZYIV
      XOUWQVWSUXEVXFQVXOUWPVWRCUQVXOUWOVWQTPVXOUSUWMVWOLVXPYKRZYLVXOUWMVWOUXDVX
      EAVXPVXOUXDVXEOUWRUWMIVXOUWSVWTUXCVXDVBVXOUWPVWRUWRUTVXQRVXOUWTVXAUXBVXCV
      AUWRUWLVWNYMVXOUXAVXBTPVXOUWPVWRUWRQVXQRRSSXAYNSYOUUGVVSTVWJTUUHUUIUUJVWB
      VWIQUUKUULUUMWKUWCVWKUXTTVWJVDZUYLUWAVWKVXROUWBUWAVVSUXTTVWJBUUNUUOXAUWAU
      WBVXRUYLOZUWABUUPIZUXTUUQUWBVXSUVFZBUURVXTVYAUXTVXTUWBVXSVXTUWBUHZUXTVWJU
      YKTVYBVWBUYAVWIUYJQVYBVWABCUQVXTVWABOZUWBVXTVWAUYBTPJZBVXTVVTUYBTPVXTUYBV
      UJIVVTUYBOVXTUYBHVUJBUUSWIUUTVJUYBUVAUVBRVXTBUGITUGIVYDBOBUVCUVDBTUVEUVGY
      PZXAYLVYBUYCVWHUYIAVYBUWRUYCIZUHZVWCUYDVWGUYHVBVYGVWABUWRUTVXTVYCUWBVYFVY
      EUVHZRVYGVWDUYEVWFUYGVAVYGVWDUWRVVOMZUYEVYFVWDVYIOVYBUWRUYCVVOUVIUVJUWBVY
      FUYEVYIOZVXTVYFUWBVYJVYFUWRHIUWBVYJUWRUYBYHUAUBUWRCHUGUYMUWFUWIUWJUWKGNUW
      NTUYOVVEQJZVDZVEZMZOZFUWEUMZVFZEVGZDVHZVIZMVYIUIUWRVYTMUYMUWRVYTVKVUFUWRV
      YTVVOVUFVYSVVNVUFVYRVVMDVUFVYQVVLEVUFVYPVVKUWFUWIVUFVYOVVJFUWEVUFVYNVVIUW
      JVUFUWKVYMVVHVUFGNVYLNVVGVUGVUFUWNVYKVVFTVUFUYOUWQVVEQVUHRVOWDVPVQVRVSVTW
      AWBVPUBFDGUFUAEWCUWRVVOWEWFUVKUVLUVMYQVYGVWEUYFTPVYGVWABUWRQVYHRRSSUVNSVO
      UVQUXTVXSUWBUXTVXRTUYLUXTTVWJYRUXTTUYKYRYQUVRUVOUVPUVSYPYSYSYPUVT $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at zero.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bernpoly0 $p |- ( X e. CC -> ( 0 BernPoly X ) = 1 ) $=
      ( vk cc wcel cc0 cbernpoly co wceq c1 cexp cmin cfz cv cbc caddc cdiv csu
      cmul cif cn0 0nn0 bernpolyval mpan eqid1 iftrue ax-mp syl6eq ) ACDZEAFGZE
      EHZIAEJGEEIKGLGEBMZNGUKAFGEUKKGIOGPGRGBQKGZSZIETDUHUIUMHUABEAUBUCUJUMIHEU
      DUJIULUEUFUG $.
      $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.
    $( The value of the Bernoulli polynomials at natural numbers.  (Contributed
       by Scott Fenton, 16-May-2014.) $)
    bernpolynn $p |- ( ( N e. NN /\ X e. CC ) ->
       ( N BernPoly X ) = ( ( X ^ N ) -
              sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x.
                ( ( k BernPoly X ) / ( ( N - k ) + 1 ) ) ) ) ) $=
      ( cn wcel cc wa cbernpoly co cc0 wceq c1 cexp cmin cfz cv caddc cdiv cmul
      cbc csu cif cn0 nnnn0 bernpolyval sylan wne nnne0 df-ne sylib iffalse syl
      wn adantr eqtrd ) BDEZCFEZGBCHIZBJKZLCBMIJBLNIOIBAPZTIUTCHIBUTNILQIRISIAU
      ANIZUBZVAUPBUCEUQURVBKBUDABCUEUFUPVBVAKZUQUPUSUMZVCUPBJUGVDBUHBJUIUJUSLVA
      UKULUNUO $.
      $( [16-May-2014] $)
  $}

  ${
    $d X k $.
    $( The value of the Bernoulli polynomials at one.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bernpoly1 $p |- ( X e. CC -> ( 1 BernPoly X ) = ( X - ( 1 / 2 ) ) ) $=
      ( vk cc wcel c1 cbernpoly co cc0 cdiv cmul cmin cfz cbc caddc wceq ax-1cn
      c2 opreq2i opreq12d syl6eq cexp cv csu 1nn bernpolynn mpan subidi sumeq1i
      cn cvv cz oprex opreq2 opreq1 opreq1d cn0 1nn0 bcn0 ax-mp subid1i opreq1i
      0z df-2 eqtr4i opreq12i fsum1i mp2an eqtri exp1 bernpoly0 opreq2d mulid2i
      halfcl eqtrd ) ACDZEAFGZAEUAGZEHAFGZQIGZJGZKGZAEQIGZKGVOVPVQHEEKGZLGZEBUB
      ZMGZWEAFGZEWEKGZENGZIGZJGZBUCZKGZWAEUIDVOVPWMOUDBEAUEUFWLVTVQKWLHHLGZWKBU
      CZVTWDWNWKBWCHHLEPUGRUHVTUJDHUKDWOVTOEVSJULVBWKVTUJBHWEHOZWKEHMGZVREHKGZE
      NGZIGZJGVTWPWFWQWJWTJWEHEMUMWPWGVRWIWSIWEHAFUNWPWHWRENWEHEKUMUOSSWQEWTVSJ
      EUPDWQEOUQEURUSWSQVRIWSEENGQWREENEPUTVAVCVDRVETVFVGVHRTVOVQAVTWBKAVIVOVTE
      WBJGWBVOVSWBEJVOVREQIAVJUOVKWBECDWBCDPEVMUSVLTSVN $.
      $( [16-May-2014] $)
  $}

  ${
    $d N k n $.  $d X k n $.
    $( Closure law for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.) $)
    bernpolycl $p |- ( ( N e. NN0 /\ X e. CC ) -> ( N BernPoly X ) e. CC ) $=
      ( vn vk cn0 wcel cc cbernpoly co wi opreq1 eleq1d wceq cc0 adantr syl wbr
      c1 wa syl2anc cv weq imbi2d cmin cfz wral r19.21v cn elnn0 w3a cexp caddc
      wo cbc cdiv cmul csu bernpolynn 3adant3 nnnn0 expcl sylan2 ancoms cuz cfv
      nnm1nn0 nn0uz syl6eleq 3ad2ant1 cz elfzelz bccl syl2an wne simprr clt nnz
      nn0cn zsubcl cr elfznn0 ad2antrl nnre ad2antrr peano2rem cle elfzle2 ltm1
      nn0re lelttrd wb posdif mpbid elnnz sylanbrc peano2nn nnne0 divcl syl3anc
      nncn mulcl ralimdva 3impia fsumcl subcl eqeltrd 3expib bernpoly0 syl6eqel
      expr ax-1cn syl5ibr jaoi sylbi exp3a a2d syl5bi nn0sinds imp ) AEFBGFZABH
      IZGFZXTCUAZBHIZGFZJZXTDUAZBHIZGFZJZXTYBJCDACDUBZYEYIXTYKYDYHGYCYGBHKLUCYC
      AMZYEYBXTYLYDYAGYCABHKLUCYJDNYCRUDIZUEIZUFXTYIDYNUFZJYCEFZYFXTYIDYNUGYPXT
      YOYEYPXTYOYEYPYCUHFZYCNMZUMXTYOSZYEJZYCUIYQYTYRYQXTYOYEYQXTYOUJZYDBYCUKIZ
      YNYCYGUNIZYHYCYGUDIZRULIZUOIZUPIZDUQZUDIZGYQXTYDUUIMYODYCBURUSUUAUUBGFZUU
      HGFZUUIGFYQXTUUJYOXTYQUUJYQXTYPUUJYCUTZBYCVAVBVCUSUUAYMNVDVEZFZUUGGFZDYNU
      FZUUKYQXTUUNYOYQYMEUUMYCVFVGVHVIYQXTYOUUPYQXTSZYIUUODYNUUQYGYNFZYIUUOUUQU
      URYISZSZUUCGFZUUFGFZUUOUUTUUCEFZUVAUUQYPYGVJFZUVCUUSYQYPXTUULOUURUVDYIYGN
      YMVKOZYGYCVLVMUUCVRPUUTYIUUEGFZUUENVNZUVBUUQUURYIVOUUTUUEUHFZUVFUUTUUDUHF
      ZUVHUUTUUDVJFZNUUDVPQZUVIUUQYCVJFZUVDUVJUUSYQUVLXTYCVQOUVEYCYGVSVMUUTYGYC
      VPQZUVKUUTYGYMYCUURYGVTFZUUQYIUURYGEFUVNYGYMWAYGWIPWBZUUTYCVTFZYMVTFYQUVP
      XTUUSYCWCWDZYCWEPUVQUURYGYMWFQUUQYIYGNYMWGWBUUTUVPYMYCVPQUVQYCWHPWJUUTUVN
      UVPUVMUVKWKUVOUVQYGYCWLTWMUUDWNWOUUDWPPZUUEWTPUUTUVHUVGUVRUUEWQPYHUUEWRWS
      UUCUUFXATXJXBXCUUGDNYMXDTUUBUUHXETXFXGYSYEYRNBHIZGFZXTUVTYOXTUVSRGBXHXKXI
      OYRYDUVSGYCNBHKLXLXMXNXOXPXQXRXS $.
      $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.
    $( A sum for Bernoulli polynomials.  (Contributed by Scott Fenton,
       16-May-2014.) $)
    bpolysum $p |- ( ( N e. NN0 /\ X e. CC ) ->
      sum_ k e. ( 0 ... N ) ( ( N _C k ) x. ( ( k BernPoly X ) / ( ( N - k ) +
        1 ) ) ) = ( X ^ N ) ) $=
      ( wcel cc cc0 cfz co cbernpoly cmin caddc cdiv cmul wceq opreq1d opreq12d
      cbc c1 syl opreq2d cn0 cv csu cexp cn wo wi elnn0 wa cuz nnm1nn0 syl6eleq
      cfv nn0uz adantr oprex opreq2 opreq1 fsump1i ax-1cn npcan sylancl sumeq1d
      nncn nnnn0 bcnn eqtrd subid addid2i syl6eq bernpolycl div1 mulid2 3eqtrrd
      sylan bernpolynn wral elfzelz bccl syl2an nn0cn wne elfznn0 simpr syl2anr
      cz clt wbr nnz zsubcl zre adantl nnre ad2antrr peano2rem cle elfzle2 ltm1
      cr lelttrd posdif syl2anc mpbid elnnz sylanbrc peano2nn nnne0 divcl mulcl
      wb syl3anc ralrimiva fsumcl id1 expcl pncan3 eqtr3d 3eqtr3d div1i opreq2i
      ex mulid2i eqtri bernpoly0 exp0 3eqtr4a sumeq12dv cvv 0z fsum1i 0nn0 bcn0
      mp2an ax-mp 0cnALT subidi opreq1i opreq12i eqeq12d syl5ibr jaoi sylbi imp
      ) BUADZCEDZFBGHZBAUBZQHZUUGCIHZBUUGJHZRKHZLHZMHZAUCZCBUDHZNZUUDBUEDZBFNZU
      FUUEUUPUGZBUHUUQUUSUURUUQUUEUUPUUQUUEUIZFBRJHZRKHZGHZUUMAUCZFUVAGHZUUMAUC
      ZBUVBQHZUVBCIHZBUVBJHZRKHZLHZMHZKHZUUNUUOUUTUVAFUJUMZDZUVDUVMNUUQUVOUUEUU
      QUVAUAUVNBUKUNULUOZUUMUVLAFUVAUUHUULMUPUVGUVKMUPUUGUVBNZUUHUVGUULUVKMUUGU
      VBBQUQUVQUUIUVHUUKUVJLUUGUVBCIURUVQUUJUVIRKUUGUVBBJUQOPPUSSUUQUVDUUNNUUEU
      UQUVCUUFUUMAUUQUVBBFGUUQBEDZREDUVBBNBVDZUTBRVAVBZTVCUOUUTUVFUUOUVFJHZKHZU
      VMUUOUUTUWAUVLUVFKUUTUVLRBCIHZRLHZMHZUWCUWAUUQUVLUWENUUEUUQUVGRUVKUWDMUUQ
      UVGBBQHZRUUQUVBBBQUVTTUUQUUDUWFRNBVEZBVFSVGUUQUVHUWCUVJRLUUQUVBBCIUVTOUUQ
      UVJBBJHZRKHZRUUQUVIUWHRKUUQUVBBBJUVTTOUUQUWIFRKHZRUUQUWHFRKUUQUVRUWHFNUVS
      BVHSORUTVIZVJVGPPUOUUTUWERUWCMHZUWCUUTUWDUWCRMUUTUWCEDZUWDUWCNUUQUUDUUEUW
      MUWGBCVKVOZUWCVLSTUUTUWMUWLUWCNUWNUWCVMSVGABCVPVNTUUTUVFEDZUUOEDZUWBUUONU
      UTUVOUUMEDZAUVEVQUWOUVPUUTUWQAUVEUUTUUGUVEDZUIZUUHEDZUULEDZUWQUWSUUHUADZU
      WTUUTUUDUUGWFDZUXBUWRUUQUUDUUEUWGUOUUGFUVAVRZUUGBVSVTUUHWASUWSUUIEDZUUKED
      ZUUKFWBZUXAUWRUUGUADUUEUXEUUTUUGUVAWCUUQUUEWDUUGCVKWEUWSUUKUEDZUXFUWSUUJU
      EDZUXHUWSUUJWFDZFUUJWGWHZUXIUUTBWFDZUXCUXJUWRUUQUXLUUEBWIUOUXDBUUGWJVTUWS
      UUGBWGWHZUXKUWSUUGUVABUWRUUGWSDZUUTUWRUXCUXNUXDUUGWKSWLZUWSBWSDZUVAWSDUUQ
      UXPUUEUWRBWMWNZBWOSUXQUWRUUGUVAWPWHUUTUUGFUVAWQWLUWSUXPUVABWGWHUXQBWRSWTU
      WSUXNUXPUXMUXKXJUXOUXQUUGBXAXBXCUUJXDXEUUJXFSZUUKVDSUWSUXHUXGUXRUUKXGSUUI
      UUKXHXKUUHUULXIXBXLUUMAFUVAXMXBUUEUUEUUDUWPUUQUUEXNUWGCBXOWEUVFUUOXPXBXQX
      RYAUUEUUPUURRFCIHZRLHZMHZCFUDHZNUUERRRLHZMHZRUYAUYBUYDRRMHRUYCRRMRUTXSXTR
      UTYBYCUUEUXTUYCRMUUEUXSRRLCYDOTCYEYFUURUUNUYAUUOUYBUURUUNFFGHZFUUGQHZUUIF
      UUGJHZRKHZLHZMHZAUCZUYAUURUUFUYEUUMUYJABFFGUQUURUUMUYJNUUGUUFDUURUUHUYFUU
      LUYIMBFUUGQURUURUUKUYHUUILUURUUJUYGRKBFUUGJUROTPUOYGUYKFFQHZUXSFFJHZRKHZL
      HZMHZUYAUYPYHDFWFDUYKUYPNUYLUYOMUPYIUYJUYPYHAFUUGFNZUYFUYLUYIUYOMUUGFFQUQ
      UYQUUIUXSUYHUYNLUUGFCIURUYQUYGUYMRKUUGFFJUQOPPYJYMUYLRUYOUXTMFUADUYLRNYKF
      YLYNUYNRUXSLUYNUWJRUYMFRKFYOYPYQUWKYCXTYRYCVJBFCUDUQYSYTUUAUUBUUC $.
      $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.
    $( Lemma for ~ bpolydif .  Calculate a trivial case.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpolydiflem1 $p |- ( N = 1 -> ( ( ph /\ X e. CC /\ ps ) -> ( ( ( ( X + 1 )
        ^ N ) - ( X ^ N ) ) - sum_ k e. ( 0 ... ( N - 1 ) ) ( ( N _C k ) x. ( (
        ( k BernPoly ( X + 1 ) ) - ( k BernPoly X ) ) / ( ( N - k ) + 1 ) ) ) )
        = ( N x. ( X ^ ( N - 1 ) ) ) ) ) $=
      ( wcel c1 caddc co cexp cmin cc0 cbc cbernpoly cdiv cmul wceq c2 opreq12d
      syl6eq cc w3a cfz csu ax-1cn subid1i peano2cn exp1 syl pncan2 mpan2 eqtrd
      cv bernpoly0 opreq1d opreq2d subidi opreq1i 2cn 2ne0 div0i opreq2i mul01i
      eqtri exp0 mulid1i 3eqtr4a 3ad2ant2 opreq2 opreq1 adantr sumeq12dv cvv cz
      oprex 0z df-2 syl6eqr cn0 1nn0 bcn0 ax-mp fsum1i mp2an id eqeq12d syl5ibr
      ) AEUAFZBUBEGHIZDJIZEDJIZKIZLDGKIZUCIZDCUMZMIZWOWINIZWOENIZKIZDWOKIZGHIZO
      IZPIZCUDZKIZDEWMJIZPIZQDGQZWIGJIZEGJIZKIZGLWINIZLENIZKIZROIZPIZKIZGELJIZP
      IZQZWHAXTBWHGLKIZGXQXSGUEUFZWHXKGXPLKWHXKWIEKIZGWHXIWIXJEKWHWIUAFZXIWIQEU
      GZWIUHUIEUHSWHGUAFYCGQUEEGUJUKULWHXPGGGKIZROIZPIZLWHXOYGGPWHXNYFROWHXLGXM
      GKWHYDXLGQYEWIUNUIEUNSUOUPYHGLPILYGLGPYGLROILYFLROGUEUQZURRUSUTVAVDVBGUEV
      CVDTSWHXSGGPIGWHXRGGPEVEUPGUEVFTVGVHXHXEXQXGXSXHWLXKXDXPKXHWJXIWKXJKDGWIJ
      VIDGEJVISXHXDLLUCIZGWOMIZWSGWOKIZGHIZOIZPIZCUDZXPXHWNYJXCYOCXHWMLLUCXHWMY
      FLDGGKVJYITZUPXHXCYOQWOWNFXHWPYKXBYNPDGWOMVJXHXAYMWSOXHWTYLGHDGWOKVJUOUPS
      VKVLXPVMFLVNFYPXPQGXOPVOVPYOXPVMCLWOLQZYOGLMIZXOPIXPYRYKYSYNXOPWOLGMVIYRW
      SXNYMROYRWQXLWRXMKWOLWINVJWOLENVJSYRYMGGHIRYRYLGGHYRYLYAGWOLGKVIYBTUOVQVR
      SSYSGXOPGVSFYSGQVTGWAWBURTWCWDTSXHDGXFXRPXHWEXHWMLEJYQUPSWFWG $.
      $( [16-May-2014] $)
  $}

  ${
    $d N k $.  $d X k $.  $d k m $.  $d k n $.  $d m n $.  $d N m $.  $d X m $.
    $d X n $.  $d j k $.  $d j x $.  $d k x $.  $d N j $.  $d N x $.  $d X j $.
    $d X x $.

    $( Lemma for ~ bpolydif .  Calculate a summation.  (Contributed by Scott
       Fenton, 16-May-2014.) $)
    bpolydiflem2 $p |- ( ( N e. ( ZZ>= ` 2 ) /\ X e. CC ) -> sum_ k e. ( 1 ...
        ( N - 1 ) ) ( ( N _C ( k - 1 ) ) x. ( X ^ ( k - 1 ) ) ) = ( ( ( ( X + 1
        ) ^ N ) - ( X ^ N ) ) - ( N x. ( X ^ ( N - 1 ) ) ) ) ) $=
      ( vj wcel cc c1 cmin co cfz cbc cexp cmul wceq adantr cn0 cc0 syl opreq2d
      caddc vx c2 cuz cfv wa cv csu csb wral cn uz2m1nn nnuz syl6eleq wss nn0uz
      2nn0 eleqtri uzss ax-mp sseli syl6eleqr elfzelz peano2zm bccl nn0cn simpr
      cz syl2an elfznn nnm1nn0 expcl mulcl ralrimiva 1z fsumshftm mp3an2 ax-1cn
      syl2anc subidi opreq1i oprex ax-17 opreq1 csbief zcn pncan sylancl opreq2
      opreq12d syl5eq sumeq12i binom1 sylan opreq1d 2nn fsump1i eluzelz sumeq1d
      npcan bcnn sylan2 ancoms mulid2 3eqtrd 3eqtr3d elfznn0 fsumcl bcnm1 eqtrd
      nncn nnnn0 3eqtrrd ) BUBUCUDZEZCFEZUEZGBGHIZJIZBAUFZGHIZKIZCXTLIZMIZAUGZG
      GHIZXQGHIZJIZADUFZGTIZYCUHZDUGZCGTIBLIZCBLIZHIZBCXQLIZMIZHIZXPXQGUCUDZEZY
      CFEZAXRUIZYDYKNZXNYSXOXNXQUJYRBUKZULUMOXPYTAXRXPXSXREZUEZYAFEZYBFEZYTUUEY
      APEZUUFXPBPEZXTVGEZUUHUUDXNUUIXOXNBQUCUDZPXMUUKBUBUUKEXMUUKUNUBPUUKUPUOUQ
      QUBURUSUTUOVAZOZUUDXSVGEUUJXSGXQVBXSVCRXTBVDVHYAVERXPXOXTPEZUUGUUDXNXOVFZ
      UUDXSUJEUUNXSXQVIXSVJRCXTVKVHYAYBVLVRVMYSGVGEUUAUUBVNYCADGGXQVOVPVRXPYKQY
      FJIZBYHKIZCYHLIZMIZDUGZYQYGUUPYJUUSDYEQYFJGVQVSVTYHYGEZYJBYIGHIZKIZCUVBLI
      ZMIZUUSAUAYIYCUVEYHGTWAUAUFUVEEAWBXSYINZYAUVCYBUVDMUVFXTUVBBKXSYIGHWCZSUV
      FXTUVBCLUVGSWIWDUVAUVBYHNZUVEUUSNUVAYHFEZGFEZUVHUVAYHVGEZUVIYHYEYFVBYHWER
      VQYHGWFWGUVHUVCUUQUVDUURMUVBYHBKWHUVBYHCLWHWIRWJWKXPYQQBJIZUUSDUGZYMHIZYP
      HIUUTYPTIZYPHIZUUTXPYNUVNYPHXPYLUVMYMHXNUUIXOYLUVMNUULCDBWLWMWNWNXPUVNUVO
      YPHXPUVNQXQJIZUUSDUGZYMTIZYMHIZUVRUVOXPUVMUVSYMHXPQXQGTIZJIZUUSDUGZUVRBUW
      AKIZCUWALIZMIZTIZUVMUVSXPXQUUKEZUWCUWGNXNUWHXOXNXQPUUKXNBUJEXQPEZXNBYRUJX
      MYRBUBYREXMYRUNUBUJYRWOULUQGUBURUSUTULVABVJRUOUMOZUUSUWFDQXQUUQUURMWAZUWD
      UWEMWAYHUWANUUQUWDUURUWEMYHUWABKWHYHUWACLWHWIWPRXPUWBUVLUUSDXPUWABQJXNUWA
      BNZXOXNBFEZUVJUWLXNBVGEUWMUBBWQBWERZVQBGWSWGOZSWRXPUWFYMUVRTXPUWFBBKIZYMM
      IGYMMIZYMXPUWDUWPUWEYMMXPUWABBKUWOSXPUWABCLUWOSWIXPUWPGYMMXPUUIUWPGNUUMBW
      TRWNXPYMFEZUWQYMNXOXNUWRXNXOUUIUWRUULCBVKXAXBZYMXCRXDSXEWNXPUVRFEZUWRUVTU
      VRNXPUWHUUSFEZDUVQUIUWTUWJXPUXADUVQXPYHUVQEZUEZUUQFEZUURFEZUXAUXCUUQPEZUX
      DXPUUIUVKUXFUXBUUMYHQXQVBYHBVDZVHUUQVEZRXPXOYHPEZUXEUXBUUOYHXQXFCYHVKZVHU
      UQUURVLZVRVMUUSDQXQXGVRUWSUVRYMWFVRXPQYFGTIZJIZUUSDUGZUUTBUXLKIZCUXLLIZMI
      ZTIZUVRUVOXPYFUUKEZUXNUXRNXNUXSXOXNYFPUUKXNXQUJEZYFPEUUCXQVJRUOUMOZUUSUXQ
      DQYFUWKUXOUXPMWAYHUXLNUUQUXOUURUXPMYHUXLBKWHYHUXLCLWHWIWPRXNUXNUVRNXOXNUX
      MUVQUUSDXNUXLXQQJXNXQFEZUVJUXLXQNZXNUXTUYBUUCXQXJRVQXQGWSWGZSWROXPUXQYPUU
      TTXPUXOBUXPYOMXPUXOBXQKIZBXPUXLXQBKXNUYCXOUYDOZSXNUYEBNZXOXNUUIUYGUULBXHR
      OXIXPUXLXQCLUYFSWISXEXDWNXPUUTFEZYPFEZUVPUUTNXPUXSUXADUUPUIUYHUYAXPUXADUU
      PXPYHUUPEZUEZUXDUXEUXAUYKUXFUXDXPUUIUVKUXFUYJUUMYHQYFVBUXGVHUXHRXPXOUXIUX
      EUYJUUOYHYFXFUXJVHUXKVRVMUUSDQYFXGVRXPUWMYOFEZUYIXNUWMXOUWNOXOXNUYLXNXOUW
      IUYLXNUXTUWIUUCXQXKRCXQVKXAXBBYOVLVRUUTYPWFVRXLWJXI $.
      $( [16-May-2014] $)
  $}

  ${
    $d k m $.  $d k n $.  $d m n $.  $d N m $.  $d X k $.  $d X m $.  $d X n $.
    $d N k $.  $d N n $.  $d n x $.  $d X x $.
    $( Calculate the difference between successive values of the Bernoulli
       polynomials.  (Contributed by Scott Fenton, 16-May-2014.) $)
    bpolydif $p |- ( ( N e. NN /\ X e. CC ) ->
       ( ( N BernPoly ( X + 1 ) ) - ( N BernPoly X ) ) =
       ( N x. ( X ^ ( N - 1 ) ) ) ) $=
      ( vk wcel cc c1 caddc co cbernpoly cmin cmul wceq opreq12d opreq2d adantr
      cc0 cdiv syl syl3anc syl2anc vn cn cv wi weq opreq1 id eqeq12d imbi2d cfz
      cexp wral r19.21v w3a cbc csu wa cn0 peano2cn nnnn0 expcl syl2anr cuz cfv
      nnm1nn0 nn0uz syl6eleq cz elfzelz bccl syl2an nn0cn wne adantl bernpolycl
      elfznn0 clt wbr nnz zsubcl cr zre nnre ad2antrr peano2rem elfzle2 lelttrd
      cle ltm1 posdif mpbid elnnz sylanbrc peano2nn nnne0 divcl mulcl ralrimiva
      wb nncn fsumcl simpr sub4 syl22anc divsubdir syl112anc subdi sumeq2dv jca
      eqtrd fsumsub bernpolynn 3eqtr4rd 3adant3 bpolydiflem1 3ad2ant1 1re ltlen
      sylan2 bitr3d sylancr nnge1 simp3 mpbi2and subcl opreq2 opreq1d bernpoly0
      ax-1cn syl6eq cvv wss 3eqtrd 3expa 3adantl3 peano2zm eluz mpbird imp c2
      fsum1p bcn0 subidi subid1 opreqan12rd div0 mul01i addid2i opreq1i sumeq1i
      elnnnn0b nnuz 1nn0 eleqtri oprex fzss1 mp2an sseli addid2 ralimi 3ad2ant3
      a1i sumeq2d vex ancri ltle mpancom fzss2 sseld bcm1k subsub mp3an3 elfznn
      sylc zcn mulass mulcom divass divcan6 mulid1 df-2 anim1i 3adant2 nnltp1le
      mul12 1nn syl5eqbr 2z simp2 bpolydiflem2 eqcomd subsub23 expcom pm2.61ine
      mpan 3exp a2d syl5bi nnsinds ) AUBDBEDZABFGHZIHZABIHZJHZABAFJHZUKHZKHZLZU
      WTUAUCZUXAIHZUXIBIHZJHZUXIBUXIFJHZUKHZKHZLZUDZUWTCUCZUXAIHZUXRBIHZJHZUXRB
      UXRFJHZUKHZKHZLZUDZUWTUXHUDUACAUACUEZUXPUYEUWTUYGUXLUYAUXOUYDUYGUXJUXSUXK
      UXTJUXIUXRUXAIUFUXIUXRBIUFMUYGUXIUXRUXNUYCKUYGUGUYGUXMUYBBUKUXIUXRFJUFNMU
      HUIUXIALZUXPUXHUWTUYHUXLUXDUXOUXGUYHUXJUXBUXKUXCJUXIAUXAIUFUXIABIUFMUYHUX
      IAUXNUXFKUYHUGUYHUXMUXEBUKUXIAFJUFNMUHUIUYFCFUXMUJHZULUWTUYECUYIULZUDUXIU
      BDZUXQUWTUYECUYIUMUYKUWTUYJUXPUYKUWTUYJUXPUYKUWTUYJUNZUXLUXAUXIUKHZBUXIUK
      HZJHZPUXMUJHZUXIUXRUOHZUYAUXIUXRJHZFGHZQHZKHZCUPZJHZUXOUYKUWTUXLVUCLUYJUY
      KUWTUQZUYOUYPUYQUXSUYSQHZKHZCUPZUYPUYQUXTUYSQHZKHZCUPZJHZJHZUYMVUGJHZUYNV
      UJJHZJHZVUCUXLVUDUYMEDZUYNEDZVUGEDZVUJEDZVULVUOLUWTUXAEDZUXIURDZVUPUYKBUS
      ZUXIUTZUXAUXIVAVBZUWTUWTVVAVUQUYKUWTUGVVCBUXIVAVBZVUDUXMPVCVDZDZVUFEDZCUY
      PULVURUYKVVGUWTUYKUXMURVVFUXIVEZVFVGZOZVUDVVHCUYPVUDUXRUYPDZUQZUYQEDZVUEE
      DZVVHVVMUYQURDZVVNVUDVVAUXRVHDZVVPVVLUYKVVAUWTVVCOZUXRPUXMVIZUXRUXIVJVKUY
      QVLRZVVMUXSEDZUYSEDZUYSPVMZVVOVVLUXRURDZVUTVWAVUDUXRUXMVPZUWTVUTUYKVVBVNU
      XRUXAVOVBZVVMUYSUBDZVWBVVMUYRUBDZVWGVVMUYRVHDZPUYRVQVRZVWHVUDUXIVHDZVVQVW
      IVVLUYKVWKUWTUXIVSZOVVSUXIUXRVTVKVVMUXRUXIVQVRZVWJVVMUXRUXMUXIVVLUXRWADZV
      UDVVLVVQVWNVVSUXRWBRZVNVVMUXIWADZUXMWADZUYKVWPUWTVVLUXIWCZWDZUXIWEZRVWSVV
      LUXRUXMWHVRVUDUXRPUXMWFVNVVMVWPUXMUXIVQVRZVWSUXIWIZRWGVVLVWNVWPVWMVWJWSVU
      DVWOUYKVWPUWTVWROUXRUXIWJVBWKUYRWLWMUYRWNRZUYSWTRZVVMVWGVWCVXCUYSWORZUXSU
      YSWPSZUYQVUEWQTZWRVUFCPUXMXATVUDVVGVUIEDZCUYPULVUSVVKVUDVXHCUYPVVMVVNVUHE
      DZVXHVVTVVMUXTEDZVWBVWCVXIVVLVWDUWTVXJVUDVWEUYKUWTXBZUXRBVOVBZVXDVXEUXTUY
      SWPSZUYQVUHWQTZWRVUICPUXMXATUYMUYNVUGVUJXCXDVUDVUBVUKUYOJVUDVUBUYPVUFVUIJ
      HZCUPZVUKVUDUYPVUAVXOCVVMVUAUYQVUEVUHJHZKHZVXOVVMUYTVXQUYQKVVMVWAVXJVWBVW
      CUYTVXQLVWFVXLVXDVXEUXSUXTUYSXEXFNVVMVVNVVOVXIVXRVXOLVVTVXFVXMUYQVUEVUHXG
      SXJXHVUDVVGVVHVXHUQZCUYPULVXPVUKLVVKVUDVXSCUYPVVMVVHVXHVXGVXNXIWRVUFVUICP
      UXMXKTXJNVUDUXJVUMUXKVUNJUWTUYKVUTUXJVUMLVVBCUXIUXAXLXSCUXIBXLMXMXNUYLVUC
      UXOLZUDUXIFUYKUYJCUXIBXOUYLUXIFVMZVXTUYLVYAUQVUCUYOUYIVUACUPZJHZUYOUYIUYQ
      UYDUYSQHZKHZCUPZJHZUXOUYKUWTVYAVUCVYCLZUYJUYKUWTVYAVYHUYKUWTVYAUNZVUBVYBU
      YOJVYIVUBUXIPUOHZPUXAIHZPBIHZJHZUXIPJHZFGHZQHZKHZPFGHZUXMUJHZVUACUPZGHZPV
      YBGHZVYBVYIVVGPUXMVQVRZVUAEDZCUYPULZVUBWUALUYKUWTVVGVYAVVJXPVYIFUXIWHVRZV
      YAWUCVYIFWADZVWPWUFVYAUQZWUCWSXQUYKUWTVWPVYAVWRXPWUGVWPUQFUXIVQVRZWUHWUCF
      UXIXRZFUXIWJXTYAUYKUWTWUFVYAUXIYBZXPUYKUWTVYAYCYDZUYKUWTWUEVYAVUDWUDCUYPV
      VMVVNUYTEDZWUDVVTVVMUYAEDZVWBVWCWUMVVMVWAVXJWUNVWFVXLUXSUXTYETVXDVXEUYAUY
      SWPSUYQUYTWQTZWRXNVUAVYQCPUXMUXRPLZUYQVYJUYTVYPKUXRPUXIUOYFWUPUYAVYMUYSVY
      OQWUPUXSVYKUXTVYLJUXRPUXAIUFUXRPBIUFMWUPUYRVYNFGUXRPUXIJYFYGMMUUASVYIVYQP
      VYTVYBGUYKUWTVYQPLVYAVUDVYQFPKHPVUDVYJFVYPPKUYKVYJFLZUWTUYKVVAWUQVVCUXIUU
      BROVUDVYPPUXIFGHZQHZPUWTUYKVYMPVYOWURQUWTVYMFFJHPUWTVYKFVYLFJUWTVUTVYKFLV
      VBUXAYHRBYHMFYIUUCYJUYKVYNUXIFGUYKUXIEDZVYNUXILUXIWTZUXIUUDRYGUUEUYKWUSPL
      ZUWTUYKWURUBDZWVBUXIWNWVCWUREDWURPVMWVBWURWTWURWOWURUUFTROXJMFYIUUGYJXNVY
      TVYBLVYIVYSUYIVUACVYRFUXMUJFYIUUHUUIUUJUVBMVYIVYBEDZWUBVYBLVYIUXMFVCVDZDZ
      WUDCUYIULZWVDVYIUXMUBWVEVYIUXMURDZWUCUXMUBDUYKUWTWVHVYAVVIXPZWULUXMUUKWMU
      ULVGZUYKUWTWVGVYAVUDWUDCUYIUXRUYIDZVUDVVLWUDUYIUYPUXRFVVFDUXMYKDUYIUYPYLF
      URVVFUUMVFUUNUXIFJUUOFPUXMYKUUPUUQUURZWUOXSWRXNVUACFUXMXATVYBUUSRYMNYNYOU
      YLVYCVYGLZVYAUYJUYKWVMUWTUYJVYBVYFUYOJUYJUYIVUAVYECUYEVUAVYELCUYIUYEUYTVY
      DUYQKUYAUYDUYSQUFNUUTUVCNUVAOUYKUWTVYAVYGUXOLZUYJUYKUWTVYAWVNVYIVYGUYOUYI
      UXIUYBUOHZUYCKHZCUPZJHZUXOUYKUWTVYGWVRLVYAVUDVYFWVQUYOJVUDUYIVYEWVPCVUDWV
      KUQZVYEWVOUXIUYBJHZUXRQHZKHZVYDKHWVOUYSUXRQHZKHZVYDKHZWVPWVSUYQWWBVYDKWVS
      UXIYKDUXRFUXIUJHZDZUYQWWBLUAUVDVUDWVKWWGUYKWVKWWGUDUWTUYKUYIWWFUXRUYKUXIU
      XMVCVDDZUYIWWFYLUYKVWKWWHVWLVWKWWHUXMUXIWHVRZVWKVWPWWIUXIWBVWPVWQVWPUQVXA
      WWIVWPVWQVWTUVEVXBUXMUXIUVFUVNRUXMVHDVWKWWHWWIWSUXIYPUXMUXIYQUVGYRRUXMFUX
      IUVHRUVIOYSUXRUXIYKUVJYAYGWVSWWBWWDVYDKWVSWWAWWCWVOKWVSWVTUYSUXRQVUDWUTUX
      REDZWVTUYSLZWVKUYKWUTUWTWVAOWVKVVQWWJUXRFUXMVIZUXRUVORWUTWWJFEDWWKYIUXIUX
      RFUVKUVLVKYGNYGWVSWWEWVOWWCVYDKHZKHZWVPWVSWVOEDZWWCEDZVYDEDZWWEWWNLWVSWVO
      URDZWWOVUDVVAUYBVHDZWWRWVKVVRWVKVVQWWSWWLUXRYPRUYBUXIVJVKWVOVLRZWVSVWBWWJ
      UXRPVMZWWPWVKVUDVVLVWBWVLVXDXSZWVSUXRUBDZWWJWVKWXCVUDUXRUXMUVMZVNZUXRWTRZ
      WVSWXCWXAWXEUXRWORZUYSUXRWPSZWVSUYDEDZVWBVWCWWQWVSWWJUYCEDZWXIWXFVUDUWTUY
      BURDZWXJWVKVXKWVKWXCWXKWXDUXRVERBUYBVAVKZUXRUYCWQTWXBWVKVUDVVLVWCWVLVXEXS
      ZUYDUYSWPSWVOWWCVYDUVPSWVSWWMUYCWVOKWVSWWMWWCUYCUXRUYSQHZKHZKHZUYCWWCWXNK
      HZKHZUYCWVSVYDWXOWWCKWVSVYDUYCUXRKHZUYSQHZWXOWVSUYDWXSUYSQWVSWWJWXJUYDWXS
      LWXFWXLUXRUYCUVQTYGWVSWXJWWJVWBVWCWXTWXOLWXLWXFWXBWXMUYCUXRUYSUVRXFXJNWVS
      WWPWXJWXNEDZWXPWXRLWXHWXLWVSWWJVWBVWCWYAWXFWXBWXMUXRUYSWPSWWCUYCWXNUWESWV
      SWXRUYCFKHZUYCWVSWXQFUYCKWVSVWBVWCWWJWXAWXQFLWXBWXMWXFWXGUYSUXRUVSXDNWVSW
      XJWYBUYCLWXLUYCUVTRXJYMNXJYMXHNXNVYIUYOUXOJHZWVQLZWVRUXOLZVYIWVQWYCVYIUXI
      YTVCVDDZUWTWVQWYCLVYIWYFYTUXIWHVRZVYIYTFFGHZUXIWHUWAVYIWUIWYHUXIWHVRZUYKV
      YAWUIUWTUYKVYAUQZWUIWUHUYKWUFVYAWUKUWBWYJWUGVWPWUIWUHWSXQUYKVWPVYAVWROWUJ
      YAYRUWCUYKUWTWUIWYIWSZVYAFUBDUYKWYKUWFFUXIUWDUWOXPWKUWGVYIYTVHDVWKWYFWYGW
      SUWHUYKUWTVWKVYAVWLXPYTUXIYQYAYRUYKUWTVYAUWIZCUXIBUWJTUWKVYIUYOEDZUXOEDZW
      VQEDZWYDWYEWSVYIVUPVUQWYMUYKUWTVUPVYAVVDXNUYKUWTVUQVYAVVEXNUYMUYNYETVYIWU
      TUXNEDZWYNUYKUWTWUTVYAWVAXPVYIUWTWVHWYPWYLWVIBUXMVATUXIUXNWQTVYIWVFWVPEDZ
      CUYIULZWYOWVJUYKUWTWYRVYAVUDWYQCUYIWVSWWOWXJWYQWWTWXLWVOUYCWQTWRXNWVPCFUX
      MXATUYOUXOWVQUWLSWKXJYNYOYMUWMUWNXJUWPUWQUWRUWSYS $.
      $( [16-May-2014] $)
  $}

  ${
    $d K k n $.  $d M k n $.
    $( A closed-form expression for the sum of ` K ` -th powers.  (Contributed
       by Scott Fenton, 16-May-2014.) $)
    fsumkthpow $p |- ( ( K e. NN0 /\ M e. NN ) ->
       sum_ n e. ( 0 ... M ) ( n ^ K ) =
       ( ( ( ( K + 1 ) BernPoly ( M + 1 ) ) - ( ( K + 1 ) BernPoly 0 ) ) /
         ( K + 1 ) ) ) $=
      ( vk cn0 wcel wa c1 co cbernpoly cc0 cmin csu wceq cmul cc syl bernpolycl
      adantr syl2an cn caddc cdiv cfz cv cexp cneg cuz wral nnnn0 elnn0uz sylib
      cfv adantl nn0cn peano2cn elfznn0 expcl sylan ralrimiva fsummulc1 syl3anc
      ancoms peano2nn0 negsubdi2 syl2anc nn0p1nn bpolydif pncan sylancl opreq2d
      ax-1cn 3eqtrrd sumeq2dv subcl fsumneg opreq2 fsumtelescope2 negeqd 3eqtrd
      syl2anr 0cn nncn wne wb mpan2 fsumcl nnne0 jca divmul mpbird eqcomd ) BEF
      ZCUAFZGZBHUBIZCHUBIZJIZWPKJIZLIZWPUCIZKCUDIZAUEZBUFIZAMZWOXAXENZWPXEOIZWT
      NZWOXGXBWPXDOIZAMZWSWRLIZUGZWTWOCKUHUMFZWPPFZXDPFZAXBUIZXGXJNWNXMWMWNCEFX
      MCUJCUKULZUNWMXNWNWMBPFZXNBUOZBUPQSWMXPWNWMXOAXBXCXBFZWMXOXTXCPFZWMXOXTXC
      EFYAXCCUQXCUOQZXCBURUSVCUTZSXDWPAKCVAVBWOXJXBWPXCJIZWPXCHUBIZJIZLIZUGZAMZ
      XBYGAMZUGZXLWMXJYINWNWMXBXIYHAWMXTGZYHYFYDLIZWPXCWPHLIZUFIZOIZXIYLYDPFZYF
      PFZYHYMNWMWPEFZYAYQXTBVDZYBWPXCRTZWMYSYEPFZYRXTYTXTYAUUBYBXCUPQWPYERTZYDY
      FVEVFWMWPUAFZYAYMYPNXTBVGZYBWPXCVHTYLYOXDWPOYLYNBXCUFWMYNBNZXTWMXRHPFUUFX
      SVLBHVIVJSVKVKVMVNSWNXMYGPFZAXBUIYIYKNWMXQWMUUGAXBYLYQYRUUGUUAUUCYDYFVOVF
      UTYGAKCVPWAWOYJXKWNXMWPDUEZJIZPFZDKWQUDIZUIYJXKNWMXQWMUUJDUUKWMYSUUHPFZUU
      JUUHUUKFZYTUUMUUHEFUULUUHWQUQUUHUOQWPUUHRTUTUUIYDYFWSADWRKCUUHXCWPJVQUUHY
      EWPJVQUUHKWPJVQUUHWQWPJVQVRWAVSVTWOWSPFZWRPFZXLWTNWMUUNWNWMYSKPFZUUNYTWBW
      PKRZVJSWMYSWQPFZUUOWNYTWNCPFUURCWCCUPQZWPWQRZTWSWRVEVFVTWOWTPFZXEPFZXNWPK
      WDZGZXFXHWEWMYSUURUVAWNYTUUSYSUURGUUOUUNUVAUUTYSUUNUURYSUUPUUNWBUUQWFSWRW
      SVOVFTWNXMXPUVBWMXQYCXDAKCWGWAWMUVDWNWMUUDUVDUUEUUDXNUVCWPWCWPWHWIQSWTXEW
      PWJVBWKWL $.
      $( [16-May-2014] $)
  $}

$( (End of Scott Fenton's mathbox.) $)


