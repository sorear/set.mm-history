$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Transitive closure under a relationship
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Define the transitive closure class as a class $)
  $c Trcl $.
  $( PLEASE PUT DESCRIPTION HERE. $)
  ctrcl $a class Trcl ( R , A , X ) $.

  ${
    $d R a b y $.  $d A a b y $.  $d X a b y $.
    $( Define the transitive closure of a class ` X ` under a relationship and
       a class.  This class can be thought of as the "smallest" class
       containing all elements of ` A ` that are linked to ` X ` by a chain of
       ` R ` relationships.  Definition based off of Lemma 4.2 of Don Monk's
       notes for Advanced Set Theory, which can be found at
       ~ http://euclid.colorado.edu/~~monkd/settheory . $)
    df-trcl $a |- Trcl ( R , A , X ) =
                        U. ran ( rec ( { <. a , b >. |
                                 b = U_ y e. a Pred ( R , A , y ) } ,
                                 Pred ( R , A , X ) ) |` om ) $.
  $}

  ${
    $d R a b y $.  $d S a b y $.  $d A a b y $.  $d B a b y $.  $d X a b y $.
    $d Y a b y $.
    $( Equality theorem for transitive closure $)
    trcleq1 $p |- ( R = S -> Trcl ( R , A , X ) = Trcl ( S , A , X ) ) $=
      ( vb vy va wceq cv cpred ciun copab crdg com cres crn cuni ctrcl wel
      predeq1 adantr iuneq2dv eqeq2d opabbidv rdgeq1 syl rdgeq2 eqtrd reseq1
      rneqd unieqd df-trcl 3eqtr4g ) BCHZEIZFGIZABFIZJZKZHZGELZABDJZMZNOZPZQUOF
      UPACUQJZKZHZGELZACDJZMZNOZPZQABDRACDRUNVEVMUNVDVLUNVCVKHVDVLHUNVCVIVBMZVK
      UNVAVIHVCVNHUNUTVHGEUNUSVGUOUNFUPURVFUNURVFHFGSABCUQTUAUBUCUDVBVAVIUEUFUN
      VBVJHVNVKHABCDTVBVJVIUGUFUHVCVKNUIUFUJUKFABDGEULFACDGEULUM $.
      $( [2-Feb-2011] $)

    $( Equality theorem for the transitive closure $)
    trcleq2 $p |- ( A = B -> Trcl ( R , A , X ) = Trcl ( R , B , X ) ) $=
      ( vb vy va wceq cv cpred ciun copab crdg com cres crn cuni ctrcl wel
      predeq2 adantr iuneq2dv eqeq2d opabbidv rdgeq1 syl rdgeq2 eqtrd reseq1
      rneqd unieqd df-trcl 3eqtr4g ) ABHZEIZFGIZACFIZJZKZHZGELZACDJZMZNOZPZQUOF
      UPBCUQJZKZHZGELZBCDJZMZNOZPZQACDRBCDRUNVEVMUNVDVLUNVCVKHVDVLHUNVCVIVBMZVK
      UNVAVIHVCVNHUNUTVHGEUNUSVGUOUNFUPURVFUNURVFHFGSABCUQTUAUBUCUDVBVAVIUEUFUN
      VBVJHVNVKHABCDTVBVJVIUGUFUHVCVKNUIUFUJUKFACDGEULFBCDGEULUM $.
      $( [2-Feb-2011] $)

    $( Equality theorem for the transitive closure $)
    trcleq3 $p |- ( X = Y -> Trcl ( R , A , X ) = Trcl ( R , A , Y ) ) $=
      ( vb vy va wceq cv cpred ciun copab crdg com cres crn cuni ctrcl predeq3
      rdgeq2 reseq1 3syl rneqd unieqd df-trcl 3eqtr4g ) CDHZEIFGIABFIJKHGELZABC
      JZMZNOZPZQUHABDJZMZNOZPZQABCRABDRUGULUPUGUKUOUGUIUMHUJUNHUKUOHABCDSUIUMUH
      TUJUNNUAUBUCUDFABCGEUEFABDGEUEUF $.
      $( [2-Feb-2011] $)
  $}

  ${
    trcleq1d.1 $e |- ( ph -> R = S ) $.
    $( Equality deduction for the transitive closure $)
    trcleq1d $p |- ( ph -> Trcl ( R , A , X ) = Trcl ( S , A , X ) ) $=
      ( wceq ctrcl trcleq1 syl ) ACDGBCEHBDEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    trcleq2d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for the transitive closure $)
    trcleq2d $p |- ( ph -> Trcl ( R , A , X ) = Trcl ( R , B , X ) ) $=
      ( wceq ctrcl trcleq2 syl ) ABCGBDEHCDEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    trcleq3d.1 $e |- ( ph -> X = Y ) $.
    $( Equality deduction for the transitive closure $)
    trcleq3d $p |- ( ph -> Trcl ( R , A , X ) = Trcl ( R , A , Y ) ) $=
      ( wceq ctrcl trcleq3 syl ) ADEGBCDHBCEHGFBCDEIJ $.
      $( [2-Feb-2011] $)
  $}

  ${
    $d R a b y i $.  $d A a b y i $.  $d X a b y i $.  $d Y a b y i $.
    $( Technzical lemma for the properties of the transitive closure.  A set is
       an element of the transitive closure iff it is in some value of the
       underlying function. $)
    eltrcl $p |- ( Y e. Trcl ( R , A , X ) <->
                    E. i e. om Y e. ( ( rec ( { <. a , b >. |
                                        b = U_ y e. a Pred ( R , A , y ) } ,
                                        Pred ( R , A , X ) ) |` om ) ` i ) ) $=
      ( ctrcl wcel cv cpred ciun wceq copab crdg com cres crn cuni cfv cdm
      wrex df-trcl eleq2i wfun wb wfn frfnom fnfun ax-mp elunirn fndm rexeq1
      3bitri ) FBCEIZJFHKAGKBCAKLMNGHOZBCELZPQRZSTZJZFDKUSUAJZDUSUBZUCZVBDQUCZU
      PUTFABCEGHUDUEUSUFZVAVDUGUSQUHZVFURUQUIZQUSUJUKDFUSULUKVCQNZVDVEUGVGVIVHQ
      USUMUKVBDVCQUNUKUO $.
      $( [18-Feb-2011] $)
  $}

  ${
    $d A a b d e j $.  $d A a d e y $.  $d B j $.  $d i j $.  $d j y $.
    $d R a b d e j $.  $d R y $.  $d X a d e j $.
    $( Technical lemma for transitive closure properties.  All values of the
       transitive closure's underlying function are subsets of the base set $)
    trcllem1 $p |- ( Pred ( R , A , X ) e. B ->
                      ( ( rec ( { <. a , b >. |
                                        b = U_ y e. a Pred ( R , A , y ) } ,
                                        Pred ( R , A , X ) ) |` om ) ` i )
                       C_ A ) $=
      ( vj ve vd cv com wcel cpred ciun wceq copab crdg cres cfv wss wi c0
      csuc wrex wo nn0suc fveq2 sseq1d predss fr0g mpbiri syl5bir wa cvv iunss
      a1i mprgbir ax-17 wel wal hbopab1 hbrdg hbres hbfv hbrex eliun albii
      3imtr4i predeq3 cbviunv eqeq2i opabbii rdgeq1 reseq1 syl ax-mp iuneq1
      frsucopab wn 0ss frsucopabn adantl pm2.61dan adantr wb mpbird a1d
      r19.23aiva jaoi nfvres pm2.61i ) ELZMNZBDFOZCNZWNHLZAGLZBDALZOZPZQZGHRZWP
      SZMTZUAZBUBZUCZWOWNUDQZWNILZUEZQZIMUFZUGXIIWNUHXJXIXNXJXHUDXFUAZBUBZWQXJX
      GXOBWNUDXFUIUJWQXPWPBUBBDFUKWQXOWPBWPCXDULUJUMUNXMXIIMXKMNZXMUOZXHWQXRXHX
      LXFUAZBUBZXQXTXMXQJXKXFUAZBDJLZOZPZUPNZXTXQYEUOZXTYDBUBZYGYCBUBZJYAJYAYCB
      UQYHYBYANBDYBUKURUSYFXSYDBGHKWPXKJWSYCPZYDUPXFKLZWPNGUTZKIVAGUTZYJYCNZJYA
      UFZYNGVBYJYDNZYOGVBYMGJYAGJXKXFGJXEMGJWPXDXCGHJVCYBWPNGUTVDYBMNGUTVEJIVAG
      UTVFYMGUTVGJYJYAYCVHZYOYNGYPVIVJZXDWRYIQZGHRZQZXFYSWPSZMTQZXCYRGHXBYIWRAJ
      WSXAYCBDWTYBVKVLVMVNYTXEUUAQUUBWPXDYSVOXEUUAMVPVQVRZJWSYAYCVSZVTUJUMXQYEW
      AZUOZXTUDBUBZBWBZUUFXSUDBUUEXSUDQXQGHKWPXKYIYDXFYKYLYQUUCUUDWCWDUJUMWEWFX
      MXHXTWGXQXMXGXSBWNXLXFUIUJWDWHWIWJWKVQWOWAZXHWQUUIXHUUGUUHUUIXGUDBWNMXEWL
      UJUMWIWM $.
      $( [18-Feb-2011] $)
  $}

  ${
    $d R a b y z $.  $d A a b y z $.  $d X a b y z $.
    $( Assuming it exists, the predecessor class is a subset of the transitive
       closure $)
    trclpred $p |- ( Pred ( R , A , X ) e. B ->
                      Pred ( R , A , X ) C_ Trcl ( R , A , X ) ) $=
      ( vb vy va vz cpred wcel cv ciun wceq copab crdg com cres crn cuni ctrcl
      wss wbr wex c0 cfv fr0g wfun cdm wb wfn frfnom fnfun ax-mp peano1 fndm
      eleqtrri funbrfvbg mp3an12 mpbid 0ex breq1 cla4ev syl elrng mpbird
      elssuni df-trcl syl6ssr ) ACDIZBJZVIEKFGKACFKILMGENZVIOPQZRZSZACDTVJVIVMJ
      ZVIVNUAVJVOHKZVIVLUBZHUCZVJUDVIVLUBZVRVJUDVLUEVIMZVSVIBVKUFVLUGZUDVLUHZJV
      JVTVSUIVLPUJZWAVIVKUKZPVLULUMUDPWBUNWCWBPMWDPVLUOUMUPUDVIBVLUQURUSVQVSHUD
      UTVPUDVIVLVAVBVCHVIVLBVDVEVIVMVFVCFACDGEVGVH $.
      $( [18-Feb-2011] $)
  $}

  ${
    $d R a b i y z $.  $d A a b i y z $.  $d B i z $.  $d X a b i y z $.
    $( The transitive closure is a subset of the base class $)
    trclss $p |- ( Pred ( R , A , X ) e. B ->
                    Trcl ( R , A , X ) C_ A ) $=
      ( vb vy va vi vz cpred wcel cv ciun wceq copab crdg com cres crn cuni
      ctrcl wbr wex wss wi wal cfv wfn wb frfnom visset fnbrfvb mpan biimprd
      sseq1 trcllem1 syl5cbi syl9r cdm breldm fndm ax-mp syl6eleq syl5 pm2.43d
      19.23adv 19.21aiv wral unissb df-ral elrn imbi1i albii 3bitri sylibr
      df-trcl syl5ss ) ACDJZBKZELFGLACFLJMNGEOZVRPQRZSZTZAACDUAVSHLZILZWAUBZHUC
      ZWEAUDZUEZIUFZWCAUDZVSWIIVSWFWHHVSWFWHVSWDQKZWFWHUEWFWLWFWDWAUGZWENZVSWHW
      LWNWFWAQUHZWLWNWFUIVRVTUJZQWDWEWAIUKZULUMUNWNWMAUDWHVSWMWEAUOFABCHDGEUPUQ
      URWFWDWAUSZQWDWEWAHUKUTWOWRQNWPQWAVAVBVCVDVEVFVGWKWHIWBVHWEWBKZWHUEZIUFWJ
      IWBAVIWHIWBVJWTWIIWSWGWHHWEWAWQVKVLVMVNVOFACDGEVPVQ $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d R a b f g h t x y z $.  $d A a b f g h t x y z $.
    $d X a b f g h t x y z $.  $d Y a h $.
    $( The transitive closure is transitive in ` R ` and ` A ` $)
    trcltr $p |- ( ( X e. A /\ A. x e. A Pred ( R , A , x ) e. _V ) ->
                    ( Y e. Trcl ( R , A , X ) ->
                      Pred ( R , A , Y ) C_ Trcl ( R , A , X ) ) ) $=
      ( va vz vt vy vh vf vg vb wcel cv cpred cvv wral wa ciun wceq copab crdg
      com cres cfv cdm wrex crn cuni wss ctrcl wi csuc ssuni ax-17 ax17el weq
      pm3.27 iuneq1 predeq3 cbviunv syl6eq adantr eqeq12d cbvopabv rdgeq1
      ax-mp reseq1 frsucopab iunexg fvex setlikespec trcllem1 syl ssralv
      cbvsetlike syl5ib com12 adantl mpd sylancr sylan2 sseq2d ssiun2s syl5bir
      expcom imp32 wfn fnfvelrn frfnom peano2 ad2antrl sylanc exp32 fndm
      eleq2i r19.23adv df-trcl wfun wb fnfun elunirn bitri sseq2i 3imtr4g ) DBN
      ZBCAOPQNABRZSZEFOZGOZHIOZBCHOZPZTZUAZIGUBZBCDPZUCZUDUEZUFZNZFXTUGZUHZBCEP
      ZXTUIZUJZUKZEBCDULZNZYEYIUKXIYBYHFYCXIXJUDNZYBYHUMXJYCNXIYKYBYHYEXJUNZXTU
      FZUKZYMYFNZYHXIYKYBSSYEYMYFUOXIYKYBYNYKXIYBYNUMYKXISZYNYEJYABCJOZPZTZUKYB
      YPYMYSYEYKYSQNZYMYSUAXIKLMXRXJJKOZYRTZYSQXTMOZXRNKUPMFKUQUUCYSNKUPXSLOZUU
      BUAZKLUBZXRUCZUAZXTUUGUDUEUAXQUUFUAUUHXPUUEIGKLIKURZGLURZSXKUUDXOUUBUUIUU
      JUSUUIXOUUBUAUUJUUIXOHUUAXNTUUBHXLUUAXNUTHJUUAXNYRBCXMYQVAVBVCVDVEVFXRXQU
      UFVGVHXSUUGUDVIVHJUUAYAYRUTVJYAQNYRQNZJYARZYTXIJYAYRQQVKXJXTVLXIYABUKZUUL
      XIXRQNUUMABCDVMHBQCFDIGVNVOXHUUMUULUMXGUUMXHUULUUMUUKJBRUULXHUUKJYABVPAJB
      CVQVRVSVTWAWBWCWDJYAYREYEBCYQEVAWEWFWGWHYKYOXIYBXTUDWIZYLUDNYOYKUDYLXTWJX
      RXQWKZXJWLWBWMWNWOYCUDXJUUNYCUDUAUUOUDXTWPVHWQVRWRYJEYGNZYDYIYGEHBCDIGWSZ
      WQXTWTZUUPYDXAUUNUURUUOUDXTXBVHFEXTXCVHXDYIYGYEUUQXEXF $.
      $( [20-Feb-2011] $)
  $}

  ${
    $d R a b y $.  $d A a b y $.  $d X a b y $.
    $( The transitive closure of a relation exists (NOTE: this is the first
       theorem in the transitive closure series that requires infinity). $)
    trclex $p |- Trcl ( R , A , X ) e. _V $=
      ( vb vy va ctrcl cv cpred ciun wceq copab crdg com cres crn cuni cvv
      df-trcl wfn wcel frfnom omex fnex mp2an rnex uniex eqeltri ) ABCGDHEFHABE
      HIJKFDLZABCIZMNOZPZQREABCFDSULUKUKNTNRUAUKRUAUJUIUBUCNRUKUDUEUFUGUH $.
      $( [18-Feb-2011] $)
  $}

