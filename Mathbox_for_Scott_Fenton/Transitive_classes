$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Transitive classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y A $.
    $( The union of a class of transitive sets is transitive $)
    truni $p |- ( A. x e. A Tr x -> Tr U. A ) $=
      ( vy cv wtr wral wel cuni wss wi wal wcel wa dftr3 biimpi a1i ax-17
      alral syl jctird r19.26 syl6ibr ssuni r19.20si syl6 r19.20i df-ral
      ralbii ralcom4 bitr2i sylibr wrex eluni2 imbi1i r19.23v bitr4i albii
      3bitri ) ADZEZABFZCAGZCDZBHZIZJZABFZCKZVDEZVAVECUSFZABFZVHUTVJABUSBLZUTVC
      USIZVLMZCUSFZVJVLUTVMCUSFZVLCUSFZMVOVLUTVPVQUTVPJVLUTVPCUSNOPVLVLCKVQVLCQ
      VLCUSRSTVMVLCUSUAUBVNVECUSVCUSBUCUDUEUFVKVFCKZABFVHVJVRABVECUSUGUHVFACBUI
      UJUKVIVECVDFVCVDLZVEJZCKVHCVDNVECVDUGVTVGCVTVBABULZVEJVGVSWAVEAVCBUMUNVBV
      EABUOUPUQURUK $.
      $( [21-Feb-2011] $)
  $}

  ${
    $d x y A $.
    $( The successor of a transitive set is transitive $)
    trsuc2 $p |- ( Tr A -> Tr suc A ) $=
      ( vx vy wtr cv wcel csn wo wa wi wal csuc wceq trel orc syl6 a1i jaod
      eleq2 biimpac orim2i syl5 elsn orbi2i syl6ibr anbi2i syl5ib andi
      19.21aivv cun wb df-suc treq ax-mp dftr2 elun imbi12i albii bitri sylibr
      ) ADZBEZCEZFZVCAFZVCAGZFZHZIZVBAFZVBVFFZHZJZCKZBKZALZDZVAVMBCVAVDVEIZVDVG
      IZHZVLVIVAVRVDVCAMZIZHZVLVTVAWCVJVBAMZHZVLVAVRVJHWEWCVAVRWEVJVAVRVJWEAVBV
      CNVJWDOZPVJWEJVAWFQRWBVJVRWAVDVJVCAVBSTUAUBVKWDVJBAUCUDUEVSWBVRVGWAVDCAUC
      UFUDUGVDVEVGUHUGUIVQAVFUJZDZVOVPWGMVQWHUKAULVPWGUMUNWHVDVCWGFZIZVBWGFZJZC
      KZBKVOBCWGUOWMVNBWLVMCWJVIWKVLWIVHVDVCAVFUPUFVBAVFUPUQURURUSUSUT $.
      $( [21-Feb-2011] $)
  $}

  ${
    $d A x y z $.
    $( The intersection of a class of transitive sets is transitive. $)
    trint $p |- ( A. x e. A Tr x -> Tr |^| A ) $=
      ( vy cv wtr wral wel wss wi wal cint dftr3 ralbii biimpi df-ral ralcom4
      bitri sylib r19.20 19.20i syl wcel visset elint2 ssint imbi12i albii
      3bitri sylibr ) ADZEZABFZCAGZABFZCDZUJHZABFZIZCJZBKZEZULUMUPIZABFZCJZUSUL
      UPCUJFZABFZVDULVFUKVEABCUJLMNVFVBCJZABFVDVEVGABUPCUJOMVBACBPQRVCURCUMUPAB
      STUAVAUOUTHZCUTFUOUTUBZVHIZCJUSCUTLVHCUTOVJURCVIUNVHUQAUOBCUCUDAUOBUEUFUG
      UHUI $.
      $( [25-Feb-2011] $)
  $}

  ${
    $d x y z A $.
    $( If ` A ` is transitive and non-null, then ` |^| A ` is a subset of
       ` A ` $)
    trintss $p |- ( ( A =/= (/) /\ Tr A ) -> |^| A C_ A ) $=
      ( vy vx c0 wne wtr wa cint wel wral cv wcel wrex r19.2z ex trel ancomsd
      exp3a r19.23adv sylan9 visset elint2 syl5ib ssrdv ) ADEZAFZGZBAHZAUGBCIZC
      AJZBKZALZUKUHLUEUJUICAMZUFULUEUJUMUICANOUFUIULCAUFCKZALZUIULUFUIUOULAUKUN
      PQRSTCUKABUAUBUCUD $.
      $( [3-Mar-2011] $)
  $}

