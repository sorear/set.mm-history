$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Connectivity of betweenness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( If ` A ` , ` B ` , and ` C ` fall in order on a line, and ` A B ` and
     ` A C ` are congruent, then ` C = B ` .  (Contributed by Scott Fenton,
     7-Oct-2013.) $)
  endofsegid $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( ( B Btwn <. A , C >. /\ <. A , C >. Cgr <. A , B >. ) ->
      C = B ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn ccgr wceq wb simpl syl3an3br 3anidm23 wi
    wbr ccgr3 simpr1 simpr3 simpr2 cgrcom syl122anc idd axcgrrflx 3adant3r1 a1d
    biimpd 3jcad 3ancomb brcgr3 sylibrd btwnxfr sylan2d jcad 3anrot btwnswapid2
    a1i sylan2br syld ) DEFZADGHZFZBVEFZCVEFZIZJZBACKZLTZVKABKZMTZJZCVMLTZVLJZC
    BNZVJVOVPVLVJVNABCKZKACBKZKUATZVLVPVJVNVMVKMTZVNVSVTMTZIZWAVJVNWBVNWCVJVNWB
    VJVDVFVHVFVGVNWBOVDVIPVDVFVGVHUBZVDVFVGVHUCWEVDVFVGVHUDACABDUEUFUKVJVNUGVJW
    CVNVDVGVHWCVFBCDUHUIUJULVDVIWAWDOZVIVDVIVFVHVGIZWFVFVHVGUMZABCACBDUNQRUOVDV
    IVLWAJVPSZVIVDVIWGWIWHABCACBDUPQRUQVOVLSVJVLVNPVAURVIVDVHVFVGIVQVRSVHVFVGUS
    CABDUTVBVC $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  The next several lemmas introduce various
     properties of hypothetical points that end up eliminating alternatives to
     connectivity.  We begin by showing a congruence property of those
     hypothetical points. $)
  btwnconn1lem1 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ X e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , X >. /\ <. d , X >. Cgr <. D , B >. ) ) ) ) ->
     <. B , c >. Cgr <. X , C >. ) $=
    ( wcel w3a cv wne wa cop cbtwn wbr ccgr adantl btwnexch3and cee cfv simp1rr
    cn simp11 simp13 simp22 simp23 simp33 simp31 simp21 simp2ll simp2rl simp3rl
    simp12 btwncomand simp3rr cgrcomand simp2lr simp2rr cgrcomland cgrextendand
    cgrcomlrand cgrtr3and ) EUDJZAEUAUBZJZBVFJZKZCVFJZDVFJZHLZVFJZKZILZVFJZGLZV
    FJZFVFJZKZKZABMBCMNZBACOPQZBADOPQZNNZDAVLOPQZDVLOCDOZRQZNZCAVOOPQZCVOOWGRQZ
    NZNZVLAVQOPQVLVQOCBORQNZVOAFOPQZVOFODBORQZNNZKZBDVLFVOCEVEVGVHVNVTUEZVEVGVH
    VNVTUFZVIVJVKVMVTUGZVIVJVKVMVTUHZVIVNVPVRVSUIZVIVNVPVRVSUJZVIVJVKVMVTUKZWAW
    RABDVLEWSVEVGVHVNVTUOZWTXAXBWRWDWAWCWDWBWMWQUCSWRWFWAWFWHWLWEWQULSTWAWRVOCF
    EWSXDXEXCWAWRACVOFEWSXFXEXDXCWRWJWAWJWKWIWEWQUMSWRWOWAWOWPWNWEWMUNSTUPWAWRD
    BVOFEWSXAWTXDXCWAWRVOFDBEWSXDXCXAWTWRWPWAWOWPWNWEWMUQSURVCWAWRDVLVOCCDEWSXA
    XBXDXEXEXAWRWHWAWFWHWLWEWQUSSWAWRCVOCDEWSXEXDXEXAWRWKWAWJWKWIWEWQUTSVAVDVB
    $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we show that two of the hypotheticals we
     introduced in the first lemma are identical. $)
  btwnconn1lem2 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ X e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , X >. /\ <. d , X >. Cgr <. D , B >. ) ) ) ) ->
     X = b ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr adantl jca adantr cn cee cfv wne simp11
    simp1ll simp12 simp13 simp21 simp33 simp1rl simp2rl simp3rl simp31 btwnexch
    wceq wi syl122anc mpd btwnexchand axcgrrflx syl111anc simp23 simp32 simp1rr
    simp2ll simp22 simp3ll btwnexch3and wb btwncom syl13anc mpbid btwnconn1lem1
    simp3lr cgrextendand 3jca segconeq syl133anc ) EUAJZAEUBUCZJZBWAJZKZCWAJZDW
    AJZHLZWAJZKZILZWAJZGLZWAJZFWAJZKZKZABUDZBCUDZMZBACNOPZBADNOPZMZMZDAWGNZOPZD
    WGNCDNZQPZMZCAWJNOPZCWJNXFQPZMZMZWGAWLNZOPZWGWLNCBNQPZMZWJAFNZOPZWJFNDBNQPZ
    MZMZKZMZWQBXQOPZBFNZFBNZQPZMZBXMOPZBWLNYFQPZMZKZFWLUPZYCWQYHYKYBWQWPWQWRXBX
    LYAUFRYCYDYGWPYBABCFEVTWBWCWIWOUEZVTWBWCWIWOUGZVTWBWCWIWOUHZWDWEWFWHWOUIZWD
    WIWKWMWNUJZYBWTWPWTXAWSXLYAUKRZYCXIXRMZCXQOPZYBYTWPYBXIXRXIXJXHXCYAULXRXSXP
    XCXLUMSRWPYTUUAUQZYBWPVTWBWEWKWNUUBYNYOYQWDWIWKWMWNUNYRACWJFEUOURTUSZUTWPYG
    YBWPVTWCWNYGYNYPYRBFEVAVBTSYCYIYJWPYBABWGWLEYNYOYPWDWEWFWHWOVCZWDWIWKWMWNVD
    ZYCXAXEMZBXDOPZYBUUFWPYBXAXEWTXAWSXLYAVEXEXGXKXCYAVFSRWPUUFUUGUQZYBWPVTWBWC
    WFWHUUHYNYOYPWDWEWFWHWOVGUUDABDWGEUOURTUSZYBXNWPXNXOXTXCXLVHRZUTWPYBBWGWLFC
    BEYNYPUUDUUEYRYQYPWPYBABWGWLEYNYOYPUUDUUEUUIUUJVIYCCYEOPZCYFOPZWPYBABCFEYNY
    OYPYQYRYSUUCVIWPUUKUULVJZYBWPVTWEWCWNUUMYNYQYPYRCBFEVKVLTVMABCDEFGHIVNYBXOW
    PXNXOXTXCXLVORVPSVQWPYLYMUQZYBWPVTWCWNWCWBWNWMUUNYNYPYRYPYOYRUUEBFBAEFWLVRV
    STUS $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Establish the next congruence in the series. $)
  btwnconn1lem3 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
     <. B , d >. Cgr <. b , D >. ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr adantl syl122anc adantr wb cn simp11 wi
    cee cfv simp13 simp21 simp3l simp3r simp23 simp22 simp1rl simp2rl btwnexch3
    wne jca simp12 mpd simp2ll simp3ll btwncom syl13anc simp3lr cgrcomlr cgrcom
    mpbid bitrd simp2rr simp2lr cgrcomland cgrtr3 syl133anc mp2and cgrextendand
    ) EUAIZAEUDUEZIZBVPIZJZCVPIZDVPIZGKZVPIZJZHKZVPIZFKZVPIZLZJZABUOBCUOLZBACMN
    OZBADMNOZLLZDAWBMNOZDWBMCDMZPOZLZCAWEMNOZCWEMZWPPOZLZLZWBAWGMZNOZWBWGMCBMPO
    ZLWEXDNOWEWGMDBMPOLZLZJZBCWEWGWBDEVOVQVRWDWIUBZVOVQVRWDWIUFZVSVTWAWCWIUGZVS
    WDWFWHUHZVSWDWFWHUIZVSVTWAWCWIUJZVSVTWAWCWIUKZWJXILZWLWSLZCBWEMNOZXIXRWJXIW
    LWSWLWMWKXCXHULWSXAWRWNXHUMUPQWJXRXSUCZXIWJVOVQVRVTWFXTXJVOVQVRWDWIUQZXKXLX
    MABCWEEUNRSURXQWBDWGMNOZWBWGDMNOZXQWOXELZYBXIYDWJXIWOXEWOWQXBWNXHUSXEXFXGWN
    XCUTUPQWJYDYBUCZXIWJVOVQWAWCWHYEXJYAXPXOXNADWBWGEUNRSURWJYBYCTZXIWJVOWCWAWH
    YFXJXOXPXNWBDWGEVAVBSVFXQXFBCMZWGWBMZPOZXIXFWJXEXFXGWNXCVCQWJXFYITXIWJXFYHY
    GPOZYIWJVOWCWHVTVRXFYJTXJXOXNXLXKWBWGCBEVDRWJVOWHWCVRVTYJYITXJXNXOXKXLWGWBB
    CEVERVGSVFXQXAWBDMZWPPOZWTYKPOZXIXAWJWSXAWRWNXHVHQWJXIDWBCDEXJXPXOXLXPXIWQW
    JWOWQXBWNXHVIQVJWJXAYLLYMUCZXIWJVOVTWFWCWAVTWAYNXJXLXMXOXPXLXPCWEWBDCDEVKVL
    SVMVN $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Assuming ` C =/= c ` , we now attempt to force
     ` D = d ` from here out via a series of congruences. $)
  btwnconn1lem4 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
     <. d , c >. Cgr <. D , C >. ) $=
    ( wcel w3a cv wa wne cop cbtwn wbr ccgr adantl adantr wb cn cee cfv simp1rl
    ccgr3 simp2rl jca wi simp11 simp12 simp13 simp21 simp3l btwnexch3 syl122anc
    mpd simp3lr simp23 simp3r cgrcomlr cgrcom bitrd 3simpa anim1i btwnconn1lem3
    mpbid simp2rr simp22 simp2lr cgrcomland cgrtr3 syl133anc mp2and 3jca brcgr3
    mpbird simpl simpr 3anim3i 3anim1i btwnconn1lem1 syl2an axcgrrflx syl111anc
    syl3anr1 simp1l2 cofs brofs2 anbi1d 5segofs sylbird syl333anc ) EUAIZAEUBUC
    ZIZBWNIZJZCWNIZDWNIZGKZWNIZJZHKZWNIZFKZWNIZLZJZABMZBCMZCWTMZJZBACNOPZBADNOP
    ZLZLZDAWTNOPZDWTNCDNZQPZLZCAXCNOPZCXCNZXRQPZLZLZWTAXENZOPZWTXENCBNQPZLXCYFO
    PXCXENDBNQPLZLZJZLZCBXCNZOPZBYBNXEWTDNZNUEPZBWTNXECNQPZCWTNWTCNQPZLZJZXJXCW
    TNZDCNZQPZYLYNYPYSYLXMYALZYNYKUUDXHYKXMYAXMXNXLYEYJUDYAYCXTXPYJUFUGRXHUUDYN
    UHZYKXHWMWOWPWRXDUUEWMWOWPXBXGUIZWMWOWPXBXGUJWMWOWPXBXGUKZWQWRWSXAXGULZWQXB
    XDXFUMZABCXCEUNUOSUPYLYPBCNZXEWTNZQPZYMXEDNQPZYBYOQPZJZYLUULUUMUUNYLYHUULYK
    YHXHYGYHYIXPYEUQRXHYHUULTYKXHYHUUKUUJQPZUULXHWMXAXFWRWPYHUUPTUUFWQWRWSXAXGU
    RZWQXBXDXFUSZUUHUUGWTXECBEUTUOXHWMXFXAWPWRUUPUULTUUFUURUUQUUGUUHXEWTBCEVAUO
    VBSVFXPXIXJLZXOLZXHYEYJUUMXLUUSXOXIXJXKVCVDZABCDEFGHVEWEYLYCYOXRQPZUUNYKYCX
    HYAYCXTXPYJVGRXHYKDWTCDEUUFWQWRWSXAXGVHZUUQUUHUVCYKXSXHXQXSYDXPYJVIRVJXHYCU
    VBLUUNUHZYKXHWMWRXDXAWSWRWSUVDUUFUUHUUIUUQUVCUUHUVCCXCWTDCDEVKVLSVMVNXHYPUU
    OTZYKXHWMWPWRXDXFXAWSUVEUUFUUGUUHUUIUURUUQUVCBCXCXEWTDEVOVLSVPYLYQYRXHWQXBX
    DXFXFJZJUUTYEYJJYQYKXGUVFWQXBXGXDXFXFXDXFVQXDXFVRZUVGVNVSXPUUTYEYJUVAVTABCD
    EXEFGHWAWBXHYRYKXHWMWRXAYRUUFUUHUUQCWTEWCWDSUGVNYKXJXHXIXJXKXOYEYJWFRXHYTXJ
    LZUUCUHZYKXHWMWPWRXDXAXFXAWSWRUVIUUFUUGUUHUUIUUQUURUUQUVCUUHWMWPWRJXDXAXFJX
    AWSWRJJZUVHUUJUUANUUKUUBNWGPZXJLUUCUVJUVKYTXJBCXCWTXEWTDCEWHWIBCXCWTXEWTDCE
    WJWKWLSVM $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we introduce ` E ` , the intersection of
     ` C c ` and ` D d ` .  We begin by showing that it is the midpoint of
     ` C ` and ` c ` $)
  btwnconn1lem5 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
         <. E , C >. Cgr <. E , c >. ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr adantr wb cn cee cfv simprrr simp11
    ccgr3 simp22 simp33 simp31 cgr3rflx syl13anc simp2lr ad2antrl simp23 simp21
    cgrcomr cgrcom bitrd mpbid simp2rr cgrcomlrand 3simpa 3anim3i btwnconn1lem4
    syl122anc simpl syl2an cgrtr3 syl133anc mp2and jca 3jca cifs brifs2 sylbird
    wi ifscgr syl333anc mpd ) FUAJZAFUBUCZJZBWAJZKZCWAJZDWAJZHLZWAJZKZILZWAJZGL
    ZWAJZEWAJZKZKZABMBCMCWGMKBACNOPBADNOPQQZDAWGNOPZDWGNZCDNZRPZQZCAWJNOPZCWJNW
    TRPZQZQWGAWLNZOPWGWLNCBNRPQWJXFOPWJWLNDBNRPQQZKZECWGNOPZEDWJNOPZQZQZQZXJDEW
    JNNZXNUFPZDCNZWSRPZWJCNZWJWGNZRPZQZKZECNEWGNRPZXMXJXOYAWPXHXIXJUDWPXOXLWPVT
    WFWNWKXOVTWBWCWIWOUEZWDWEWFWHWOUGZWDWIWKWMWNUHZWDWIWKWMWNUIZDEWJFUJUKSXMXQX
    TXMXAXQXHXAWPXKWRXAXEWQXGULUMWPXAXQTXLWPXAWSXPRPZXQWPVTWFWHWEWFXAYHTYDYEWDW
    EWFWHWOUNZWDWEWFWHWOUOZYEDWGCDFUPVEWPVTWFWHWFWEYHXQTYDYEYIYEYJDWGDCFUQVEURS
    USXMXRXPRPZXSXPRPZXTWPXLCWJCDFYDYJYGYJYEXHXDWPXKXCXDXBWQXGUTUMVAWPWDWIWKWMQ
    ZKXHYLXLWOYMWDWIWKWMWNVBVCXHXKVFABCDFGHIVDVGWPYKYLQXTVPZXLWPVTWKWEWKWHWFWEY
    NYDYGYJYGYIYEYJWJCWJWGDCFVHVISVJVKVLWPYBYCVPZXLWPVTWFWNWKWEWFWNWKWHYOYDYEYF
    YGYJYEYFYGYIVTWFWNKWKWEWFKWNWKWHKKYBDENZXRNYPXSNVMPYCDEWJCDEWJWGFVNDEWJCDEW
    JWGFVQVOVRSVS $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Next, we show that ` E ` is the midpoint of ` D `
     and ` d ` $)
  btwnconn1lem6 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
         <. E , D >. Cgr <. E , d >. ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr jca adantr cn cee cfv simp11 simp21
    simprrl simp23 cgrrflx syl111anc simp33 simp31 simp2rr cgrcomand cgrcomrand
    simp22 ad2antrl simp2lr 3simpa 3anim3i simpl btwnconn1lem4 syl2an wi cgrtr3
    syl133anc mp2and cgrcomlrand 3jca cifs brifs ifscgr sylbird syl333anc mpd )
    FUAJZAFUBUCZJZBVPJZKZCVPJZDVPJZHLZVPJZKZILZVPJZGLZVPJZEVPJZKZKZABMBCMCWBMKB
    ACNOPBADNOPQQZDAWBNOPZDWBNZCDNZRPZQZCAWENOPZCWENZWORPZQZQWBAWGNZOPWBWGNCBNR
    PQWEXBOPWEWGNDBNRPQQZKZECWBNZOPZEDWENOPZQZQZQZXFXFQZXEXERPZEWBNZXMRPZQZWOWS
    RPZWBDNZWBWENZRPZQZKZEDNEWENRPZXJXKXOXTXJXFXFWKXDXFXGUFZYCSWKXOXIWKXLXNWKVO
    VTWCXLVOVQVRWDWJUDZVSVTWAWCWJUEZVSVTWAWCWJUGZCWBFUHUIWKVOWIWCXNYDVSWDWFWHWI
    UJZYFEWBFUHUISTXJXPXSWKXICWECDFYDYEVSWDWFWHWIUKZYEVSVTWAWCWJUOZXDWTWKXHWRWT
    WQWLXCULUPUMWKXIDWBWEWBFYDYIYFYHYFXJWNDCNZRPZWEWBNZYJRPZWNYLRPZWKXIDWBCDFYD
    YIYFYEYIXDWPWKXHWMWPXAWLXCUQUPUNWKVSWDWFWHQZKXDYMXIWJYOVSWDWFWHWIURUSXDXHUT
    ABCDFGHIVAVBWKYKYMQYNVCZXIWKVOWAWCWFWCWAVTYPYDYIYFYHYFYIYEDWBWEWBDCFVDVETVF
    VGSVHWKYAYBVCZXIWKVOVTWIWCWAVTWIWCWFYQYDYEYGYFYIYEYGYFYHVOVTWIKWCWAVTKWIWCW
    FKKYACENZXQNYRXRNVIPYBCEWBDCEWBWEFVJCEWBDCEWBWEFVKVLVMTVN $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Under our assumptions, ` C ` and ` d ` are
     distinct. $)
  btwnconn1lem7 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
        C =/= d ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr wi syl5 cee simp1l3 simp2rr simp2lr
    cfv adantr 3jca simp11 simp21 simp22 simp23 simp31 simpr1 wceq opeq2 breq1d
    cn 3anbi2d biimparc simp2 simp1 simp2l simp2r cgrid2 syl13anc opeq1 breq12d
    imp simp3l axcgrid expdimp 3ad2antr3 mpd ex necon3d syl122anc ) FUQJZAFUAUE
    ZJZBVRJZKZCVRJZDVRJZHLZVRJZKZILZVRJZGLZVRJZEVRJZKZKZABMZBCMZCWDMZKBACNOPBAD
    NOPQZQZDAWDNOPZDWDNZCDNZRPZQZCAWGNOPZCWGNZXARPZQZQZWDAWINZOPWDWINCBNRPQWGXI
    OPWGWINDBNRPQQZKZECWDNZOPEDWGNOPQZQZCWGMZXNWPXFXBKZWMXOXNWPXFXBXKWPXMWNWOWP
    WQXHXJUBUFXKXFXMXDXFXCWRXJUCUFXKXBXMWSXBXGWRXJUDUFUGWMVQWBWCWEWHXPXOSVQVSVT
    WFWLUHWAWBWCWEWLUIWAWBWCWEWLUJWAWBWCWEWLUKWAWFWHWJWKULVQWBWCQZWEWHQZKZXPXOX
    SXPQZWPXOXSWPXFXBUMXTCWGCWDXSXPCWGUNZCWDUNZXPYAQWPCCNZXARPZXBKZXSYBYAYEXPYA
    YDXFWPXBYAYCXEXARCWGCUOUPURUSXSYEYBXSYEQCDUNZYBXSYEYFYEYDXSYFWPYDXBUTXSVQWB
    WBWCYDYFSVQXQXRVAZVQWBWCXRVBZYHVQWBWCXRVCCCDFVDVETVHXSWPXBYFYBSYDXSXBYFYBXB
    YFQXLYCRPZXSYBYFYIXBYFXLWTYCXARCDWDVFCDCUOVGUSXSVQWBWEWBYIYBSYGYHVQXQWEWHVI
    YHCWDCFVJVETVKVLVMVNTVKVOVMVNVPTVH $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we introduce the last three points used in
     the construction: ` P ` , ` Q ` , and ` R ` will turn out to be equal
     further down, and will provide us with the key to the final statement.  We
     begin by establishing congruence of ` R P ` and ` E d ` $)
  btwnconn1lem8 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. R , P >. Cgr <. E , d >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr adantr cn cee cfv cv ccgr3 simpr2l simpr1r
    wne ad2antll wb simp11 simp2l1 simp31 cgrcomlr syl122anc cgrcom bitrd mpbid
    simp2r1 simp33 simp2r3 simpr1l simp2l3 btwncom syl13anc simprll wi btwnintr
    adantl mp2and cgrextendand 3jca brcgr3 syl133anc mpbird axcgrrflx syl111anc
    simpr2r simp1 simp2l simp2r simpl simprl btwnconn1lem7 syl2an necomd brofs2
    jca cofs anbi1d 5segofs sylbird syl333anc ) IUAMZAIUBUCZMZBWOMZNZCWOMZDWOMZ
    KUDZWOMZNZLUDZWOMZJUDZWOMZHWOMZNZOZEWOMZFWOMZGWOMZNZNZABUHBCUHCXAUHNBACPQRB
    ADPQROODAXAPQRDXAPCDPZSROCAXDPQRCXDPZXPSROOXAAXFPZQRXAXFPCBPSROXDXRQRXDXFPD
    BPSROONZHCXAPQRZHDXDPQRZOZCXAEPQRZCEPXQSRZOZCXDGPZQRZCGPZCHPZSRZOZGEFPQRGFP
    GEPZSROZNZOZOZOZYGXDYHPEYIPUERZXDEPEXDPSRZYDOZNZXDCUHZYLHXDPZSRZYQYGYRYTYOY
    GXOXSYGYJYEYMYBUFUIZYQYRXDCPZECPZSRZYFEHPZSRZYJNZYQUUHUUJYJYQYDUUHYOYDXOXSY
    CYDYKYMYBUGUIZXOYDUUHUJYPXOYDUUGUUFSRZUUHXOWNWSXKWSXEYDUUMUJWNWPWQXJXNUKZWS
    WTXBXIWRXNULZWRXJXKXLXMUMZUUOXEXGXHXCWRXNUSZCECXDIUNUOXOWNXKWSXEWSUUMUUHUJU
    UNUUPUUOUUQUUOECXDCIUPUOUQTURZXOYPXDCGECHIUUNUUQUUOWRXJXKXLXMUTZUUPUUOXEXGX
    HXCWRXNVAZUUEYQCEXAPQRZXTCUUIQRZYQYCUVAYOYCXOXSYCYDYKYMYBVBUIXOYCUVAUJZYPXO
    WNWSXBXKUVCUUNUUOWSWTXBXIWRXNVCZUUPCXAEIVDVETURYPXTXOXSXTYAYNVFVIXOUVAXTOUV
    BVGZYPXOWNXKWSXHXBUVEUUNUUPUUOUUTUVDECHXAIVHUOTVJUURYOYJXOXSYGYJYEYMYBVRUIZ
    VKUVFVLXOYRUUKUJZYPXOWNXEWSXMXKWSXHUVGUUNUUQUUOUUSUUPUUOUUTXDCGECHIVMVNTVOY
    QYSYDXOYSYPXOWNXEXKYSUUNUUQUUPXDEIVPVQTUULWHVLYQCXDXOWRXCXINXSYBOCXDUHYPXOW
    RXCXIWRXJXNVSWRXCXIXNVTWRXCXIXNWAVLYPXSYBXSYOWBXSYBYNWCWHABCDHIJKLWDWEWFXOU
    UAUUBOZUUDVGZYPXOWNXEWSXMXKXKWSXHXEUVIUUNUUQUUOUUSUUPUUPUUOUUTUUQWNXEWSNXMX
    KXKNWSXHXENNZUVHUUFYLPUUGUUCPWIRZUUBOUUDUVJUVKUUAUUBXDCGEECHXDIWGWJXDCGEECH
    XDIWKWLWMTVJ $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, a quick use of transitivity to establish
     congruence on ` R Q ` and ` E D ` $)
  btwnconn1lem9 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. R , Q >. Cgr <. E , D >. ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr cee cfv wne simp11 simp33 simp32 simp31
    simp2r3 simp2r1 simpr3r ad2antll btwnconn1lem8 cgrtrand simp1 simp2l simp2r
    cn simpl simprl jca btwnconn1lem6 syl2an wi simp2l2 cgrtr3 syl133anc adantr
    3jca mp2and ) IUQMZAIUAUBZMZBVKMZNZCVKMZDVKMZKOZVKMZNZLOZVKMZJOZVKMZHVKMZNZ
    PZEVKMZFVKMZGVKMZNZNZABUCBCUCCVQUCNBACQRSBADQRSPPDAVQQRSDVQQCDQZTSPCAVTQRSC
    VTQZWLTSPPVQAWBQZRSVQWBQCBQTSPVTWNRSVTWBQDBQTSPPNZHCVQQRSHDVTQRSPZCVQEQRSCE
    QWMTSPZCVTGQRSCGQCHQTSPZGEFQRSZGFQZGEQTSZPNZPZPZPWTHVTQZTSZHDQZXETSZWTXGTSZ
    WKXDGFGEHVTIVJVLVMWFWJUDZVNWFWGWHWIUEZVNWFWGWHWIUFZXKVNWFWGWHWIUGWAWCWDVSVN
    WJUHZWAWCWDVSVNWJUIZXCXAWKWOWSXAWQWRWPUJUKABCDEFGHIJKLULUMWKVNVSWENWOWPPXHX
    DWKVNVSWEVNWFWJUNVNVSWEWJUOVNVSWEWJUPVHXDWOWPWOXCURWOWPXBUSUTABCDHIJKLVAVBW
    KXFXHPXIVCZXDWKVJWIWHWDVPWDWAXOXJXKXLXMVOVPVRWEVNWJVDXMXNGFHDHVTIVEVFVGVI
    $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now we establish a congruence that will give us
     ` D = d ` when we compute ` P = Q ` later on. $)
  btwnconn1lem10 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. d , D >. Cgr <. P , Q >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wb cn cee cfv cv wne simp11 simp2r1 simp31
    simp2r3 simp2l2 simp33 simp32 simprlr adantl btwncom syl13anc mpbid simpr3l
    adantr ad2antll btwnconn1lem8 cgrcomlr syl122anc cgrcom bitrd btwnconn1lem9
    cgrcomand cgrextendand ) IUAMZAIUBUCZMZBVJMZNZCVJMZDVJMZKUDZVJMZNZLUDZVJMZJ
    UDZVJMZHVJMZNZOZEVJMZFVJMZGVJMZNZNZABUEBCUECVPUENBACPQRBADPQROODAVPPQRDVPPC
    DPZSROCAVSPQRCVSPZWKSROOVPAWAPZQRVPWAPCBPSROVSWMQRVSWAPDBPSROONZHCVPPQRZHDV
    SPQRZOZCVPEPQRCEPWLSROZCVSGPQRCGPCHPSROZGEFPQRZGFPGEPZSRZONZOZOZVSHDEGFIVIV
    KVLWEWIUFZVTWBWCVRVMWIUGZVTWBWCVRVMWIUIZVNVOVQWDVMWIUJZVMWEWFWGWHUHZVMWEWFW
    GWHUKZVMWEWFWGWHULZWJXEOZWPHVSDPQRZXEWPWJWNWOWPXCUMUNWJWPXNTZXEWJVIWCVOVTXO
    XFXHXIXGHDVSIUOUPUSUQXDWTWJWNWTXBWRWSWQURUTXMXAHVSPSRZVSHPZEGPZSRZABCDEFGHI
    JKLVAWJXPXSTXEWJXPXRXQSRZXSWJVIWHWFWCVTXPXTTXFXKXJXHXGGEHVSIVBVCWJVIWFWHVTW
    CXTXSTXFXJXKXGXHEGVSHIVDVCVEUSUQWJXEGFHDIXFXKXLXHXIABCDEFGHIJKLVFVGVH $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we establish that ` D ` and ` Q ` are
     equidistant from ` C ` $)
  btwnconn1lem11 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. D , C >. Cgr <. Q , C >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wb cee cfv wne btwnconn1lem8 btwnconn1lem9
    cn cv wceq btwnconn1lem10 adantr wi simpr3r adantl simpr2r jca opeq2 breq2d
    anbi2d 3anbi12d anbi12d biimpar simpr1 simp11 simp33 simp31 simp2r1 axcgrid
    3jca opeq1 syl5 breq12d breq1d biimpac simpll simp32 simprlr simpr3 simp2l2
    syl13anc imp opeq2d simp2l1 cgrcomlr syl122anc cgrcom bitrd mpbid ex anbi1d
    3anbi23d imbi12d syl5ibcom com23 mpdd exp3a exp4d imp31 mpd simp2r3 btwncom
    ccgr3 cgrcomand brcgr3 syl133anc mpbird simpr1r ad2antll simpr cofs 5segofs
    brofs2 sylbird syl333anc ad2antrr mp2and pm2.61dane ) IUFMZAIUAUBZMZBXRMZNZ
    CXRMZDXRMZKUGZXRMZNZLUGZXRMZJUGZXRMZHXRMZNZOZEXRMZFXRMZGXRMZNZNZABUCBCUCCYD
    UCNBACPQRBADPQROODAYDPQRDYDPCDPZSROCAYGPQRCYGPZYSSROOYDAYIPZQRYDYIPCBPSROYG
    UUAQRYGYIPDBPSROONZHCYDPQRZHDYGPQRZOZCYDEPQRZCEPZYTSRZOZCYGGPQRZCGPZCHPZSRZ
    OZGEFPZQRZGFPZGEPZSRZOZNZOZOZOZDCPZFCPZSRZYGHUVDYGHUHZOUURHYGPZSRZUUQHDPZSR
    ZYGDPZUUOSRZNZUVGUVDUVOUVHUVDUVJUVLUVNABCDEFGHIJKLUDZABCDEFGHIJKLUEZABCDEFG
    HIJKLUIZVHUJYRUVCUVHUVOUVGUKZUVCUUSUUMOZYRUVHUVSUKUVCUUSUUMUVBUUSUUBUUPUUSU
    UIUUNUUEULUMUVBUUMUUBUUJUUMUUIUUTUUEUNZUMUOYRUVHUVTUVSYRUVHUVTUVOUVGUVHUVTU
    VOOZOUUSUUKYTSRZOZUURYGYGPZSRZUUQUVMSRZUVNNZOZYRUVGUVHUWIUWBUVHUWDUVTUWHUVO
    UVHUWCUUMUUSUVHYTUULUUKSYGHCUPUQURUVHUWFUVJUWGUVLUVNUVHUWEUVIUURSYGHYGVIUQU
    VHUVMUVKUUQSYGHDVIUQUSUTVAYRUWIGEUHZUVGUWIUWFYRUWJUWDUWFUWGUVNVBYRXQYPYNYHU
    WFUWJUKXQXSXTYMYQVCZYAYMYNYOYPVDZYAYMYNYOYPVEZYHYJYKYFYAYQVFZGEYGIVGVSVJYRU
    WIUWJUVGUWIUWJOUUOEEPZSRZUUHOZUWOUWESRZUUOUVMSRZUVNNZOZYRUVGUWJUWIUXAUWJUWD
    UWQUWHUWTUWJUUSUWPUWCUUHUWJUUQUUOUURUWOSGEFVIZGEEVIZVKUWJUUKUUGYTSGECUPVLUT
    UWJUWFUWRUWGUWSUVNUWJUURUWOUWESUXCVLUWJUUQUUOUVMSUXBVLUSUTVMYRUXAEFUHZUVGUX
    AUWPYRUXDUWPUUHUWTVNYRXQYNYOYNUWPUXDUKUWKUWMYAYMYNYOYPVOZUWMEFEIVGVSVJYRUXD
    UXAUVGYRUWOUWOSRZUUHOZUWRUWOUVMSRZUVMUWOSRZNZOZUVEECPZSRZUKUXDUXAUVGUKYRUXK
    UXMYRUXKOZUUHUXMYRUXFUUHUXJVPUXNUUHUUGYSSRZUXMUXNYTYSUUGSUXNYGDCYRUXKYGDUHZ
    UXKUXIYRUXPUXGUWRUXHUXIVQYRXQYHYCYNUXIUXPUKUWKUWNYBYCYEYLYAYQVRZUWMYGDEIVGV
    SVJVTWAUQYRUXOUXMTUXKYRUXOUXLUVESRZUXMYRXQYBYNYBYCUXOUXRTUWKYBYCYEYLYAYQWBZ
    UWMUXSUXQCECDIWCWDYRXQYNYBYCYBUXRUXMTUWKUWMUXSUXQUXSECDCIWEWDWFUJWFWGWHUXDU
    XKUXAUXMUVGUXDUXGUWQUXJUWTUXDUXFUWPUUHUXDUWOUUOUWOSEFEUPZVLWIUXDUXHUWSUXIUV
    NUWRUXDUWOUUOUVMSUXTVLUXDUWOUUOUVMSUXTUQWJUTUXDUXLUVFUVESEFCVIUQWKWLWMWNVJW
    OWNVJWPWMVJWQWRUVDYGHUCZOHUVMQRZYGUVKPEUUQPXARZYGCPZUXLSRZHCPZGCPZSRZOZNZUY
    AUVGUVDUYJUYAUVDUYBUYCUYIUVDUUDUYBUVCUUDYRUUBUUCUUDUVAVPUMYRUUDUYBTZUVCYRXQ
    YKYCYHUYKUWKYHYJYKYFYAYQWSZUXQUWNHDYGIWTVSUJWGUVDUYCYGHPZEGPZSRZUVNUVKUUQSR
    ZNZUVDUYOUVNUYPUVDUVJUYOUVPYRUVJUYOTUVCYRUVJUYNUYMSRZUYOYRXQYPYNYKYHUVJUYRT
    UWKUWLUWMUYLUWNGEHYGIWCWDYRXQYNYPYHYKUYRUYOTUWKUWMUWLUWNUYLEGYGHIWEWDWFUJWG
    UVRYRUVCGFHDIUWKUWLUXEUYLUXQUVQXBVHYRUYCUYQTZUVCYRXQYHYKYCYNYPYOUYSUWKUWNUY
    LUXQUWMUWLUXEYGHDEGFIXCXDUJXEUVDUYEUYHUVDUUHUYEUVBUUHYRUUBUUFUUHUUNUUTUUEXF
    XGYRUUHUYETUVCYRUUHUXLUYDSRZUYEYRXQYBYNYBYHUUHUYTTUWKUXSUWMUXSUWNCECYGIWCWD
    YRXQYNYBYHYBUYTUYETUWKUWMUXSUWNUXSECYGCIWEWDWFUJWGUVDUUMUYHUVBUUMYRUUBUWAXG
    YRUUMUYHTUVCYRUUMUYGUYFSRZUYHYRXQYBYPYBYKUUMVUATUWKUXSUWLUXSUYLCGCHIWCWDYRX
    QYPYBYKYBVUAUYHTUWKUWLUXSUYLUXSGCHCIWEWDWFUJWGUOVHUJUVDUYAXHYRUYJUYAOZUVGUK
    ZUVCUYAYRXQYHYKYCYBYNYPYOYBVUCUWKUWNUYLUXQUXSUWMUWLUXEUXSXQYHYKNYCYBYNNYPYO
    YBNNZVUBUYMUVEPUYNUVFPXIRZUYAOUVGVUDVUEUYJUYAYGHDCEGFCIXKWIYGHDCEGFCIXJXLXM
    XNXOXP $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Using a long string of invocations of ~ linecgr ,
     we show that ` D = d ` . $)
  btwnconn1lem12 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       D = d ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wi cee cfv wne wceq simp11 simp2l1 simp2l3
    simp31 simp32 simp1l3 ad2antrl simpr1l ad2antll btwncolinear1 colinearperm4
    cn ccolin sylibd syl13anc adantr mpd simp2l2 simp2r1 simpr1r btwnconn1lem11
    simp2rr cgrtrand cgrcomlrand weq simp12 simp2r2 simp1rr simp2ll btwnexchand
    cv simp13 simp3ll btwnexch3and opeq1 breq2d biimpac axbtwnid syl111anc syl5
    exp3a simp1 simp2l simp2r simpl simprl btwnconn1lem7 syl2an simp2rl simp3rl
    3jca colinearperm1 simp33 simpr2r btwnconn1lem5 opeq2 breq1d anbi1d simp2r3
    jca cgrid2 breq12d biimpar syland mp2and necon3d simpr2l simpr3r linecgrand
    colinearperm3 cgrcomand biimprd ee21 com12 adantrl simp1rl colinearperm5 ex
    an12s pm2.61ine btwnconn1lem10 biimpa axcgrid eqcomd ) IUPMZAIUAUBZMZBYJMZN
    ZCYJMZDYJMZKVOZYJMZNZLVOZYJMZJVOZYJMZHYJMZNZOZEYJMZFYJMZGYJMZNZNZABUCZBCUCZ
    CYPUCZNZBACPQRZBADPQRZOZOZDAYPPQRZDYPPCDPZSRZOZCAYSPQRZCYSPZUUTSRZOZOZYPAUU
    APZQRZYPUUAPCBPSRZOZYSUVHQRZYSUUAPZDBPSRZOZOZNZHCYPPZQRHDYSPQROZCYPEPZQRZCE
    PUVDSRZOZCYSGPQRZCGPZCHPZSRZOZGEFPZQRZGFPGEPSRZOZNZOZOZOZYSDUWPEFUDZYSDPZUW
    ISRZYSDUDZUWPEEPUWISRZUWQUUJUWOCYPEEFIYIYKYLUUEUUIUEZYNYOYQUUDYMUUIUFZYNYOY
    QUUDYMUUIUGZYMUUEUUFUUGUUHUHZUXEYMUUEUUFUUGUUHUIZUVQUUMUUJUWNUUKUULUUMUUQUV
    GUVPUJUKZUWPUWACUVTUQRZUWNUWAUUJUVQUWAUWBUWHUWLUVSULUMUUJUWAUXHTZUWOUUJYIYQ
    UUFYNUXIUXBUXDUXEUXCYIYQUUFYNNOUWAYPECPUQRUXHYPECIUNYPECIUOURUSUTVAUUJUWOCE
    CDCFIUXBUXCUXEUXCYNYOYQUUDYMUUIVBZUXCUXFUUJUWOCECYSCDIUXBUXCUXEUXCYTUUBUUCY
    RYMUUIVCZUXCUXJUWNUWBUUJUVQUWAUWBUWHUWLUVSVDUMUVQUVEUUJUWNUVCUVEUVBUURUVPVF
    UKVGUUJUWODCFCIUXBUXJUXCUXFUXCABCDEFGHIJKLVEVHVGZUWPUVTYPFPZSRZTBUUAUWPBUUA
    UDZUXNUWPUXOKJVIZUUAEPZUUAFPZSRZUXNUWPYPBUUAPZQRZUXOUXPTZUUJUWOABYPUUAIUXBY
    IYKYLUUEUUIVJZYIYKYLUUEUUIVPZUXDYTUUBUUCYRYMUUIVKZUUJUWOABDYPIUXBUYCUYDUXJU
    XDUVQUUPUUJUWNUUOUUPUUNUVGUVPVLUKUVQUUSUUJUWNUUSUVAUVFUURUVPVMUKVNUVQUVIUUJ
    UWNUVIUVJUVOUURUVGVQUKVRZUUJUYAUYBTUWOUUJUYAUXOUXPUYAUXOOYPUUAUUAPZQRZUUJUX
    PUXOUYAUYHUXOUXTUYGYPQBUUAUUAVSVTWAUUJYIYQUUBUYHUXPTUXBUXDUYEYPUUAIWBWCWDWE
    UTVAUUJUWOCYSUUAEFIUXBUXCUXKUYEUXEUXFUUJYMYRUUDNZUVQUVSOZCYSUCUWOUUJYMYRUUD
    YMUUEUUIWFYMYRUUDUUIWGYMYRUUDUUIWHWOZUWOUVQUVSUVQUWNWIUVQUVSUWMWJXDZABCDHIJ
    KLWKWLZUWPYSCUUAPQRZCUVMUQRZUUJUWOACYSUUAIUXBUYCUXCUXKUYEUVQUVCUUJUWNUVCUVE
    UVBUURUVPWMUKZUVQUVLUUJUWNUVLUVNUVKUURUVGWNUKVRUUJUYNUYOTZUWOUUJYIYNUUBYTUY
    QUXBUXCUYEUXKYIYNUUBYTNOUYNCUUAYSPUQRUYOCUUAYSIUNCUUAYSIWPURUSUTVAUXLUUJUWO
    GCYSEFIUXBYMUUEUUFUUGUUHWQZUXCUXKUXEUXFUWPUUMGCUCUXGUWPGCCYPUWPUWGHCPZHYPPZ
    SRZGCUDZCYPUDZTZUWNUWGUUJUVQUWDUWGUWCUWLUVSWRUMUUJUYIUYJVUAUWOUYKUYLABCDHIJ
    KLWSWLUUJUWGVUAOZVUDTUWOUUJVUEVUBVUCVUEVUBOCCPZUWFSRZVUAOZUUJVUCVUBVUEVUHVU
    BUWGVUGVUAVUBUWEVUFUWFSGCCWTXAXBWAUUJVUGCHUDZVUAVUCUUJYIYNYNUUCVUGVUITUXBUX
    CUXCYTUUBUUCYRYMUUIXCCCHIXEUSVUIVUAOVUFUVRSRZUUJVUCVUIVUJVUAVUIVUFUYSUVRUYT
    SCHCVSCHYPVSXFXGUUJYIYNYNYQVUJVUCTUXBUXCUXCUXDCCYPIXEUSWDXHWDWEUTXIXJVAUWPU
    WDGUVDUQRZUWNUWDUUJUVQUWDUWGUWCUWLUVSXKUMUUJUWDVUKTZUWOUUJYIYTUUHYNVULUXBUX
    KUYRUXCYIYTUUHYNNOUWDYSGCPUQRVUKYSGCIUNYSGCIXNURUSUTVAUUJUWOGFGEIUXBUYRUXFU
    YRUXEUWNUWKUUJUVQUWJUWKUWCUWHUVSXLUMXOUXLXMZXMZUXPUXNUXSUXPUVTUXQUXMUXRSYPU
    UAEVSYPUUAFVSXFXPXQXRBUUAUCZUWPUXNUUJVUOUWOUXNUUJVUOUWOOZBUUAYPEFIUXBUYDUYE
    UXDUXEUXFUUJVUOUWOWJUUJVUPOUYABUUAYPPUQRZUUJUWOUYAVUOUYFXSUUJUYAVUQTZVUPUUJ
    YIYLUUBYQVURUXBUYDUYEUXDBUUAYPIUNUSUTVAUUJUWOBEPBFPSRVUOUUJUWOCYSBEFIUXBUXC
    UXKUYDUXEUXFUYMUWPCBYSPQRZCYSBPUQRZUUJUWOABCYSIUXBUYCUYDUXCUXKUVQUUOUUJUWNU
    UOUUPUUNUVGUVPXTUKUYPVRUUJVUSVUTTZUWOUUJYIYLYTYNVVAUXBUYDUXKUXCYIYLYTYNNOVU
    SBYSCPUQRVUTBYSCIUNBYSCIYAURUSUTVAUXLVUMXMXSUUJUWOUXSVUOVUNXSXMYCYBYDXMUUJU
    XAUWQTZUWOUUJYIUUFUUFUUGVVBUXBUXEUXEUXFEEFIXEUSUTVAABCDEFGHIJKLYEUUJUWQUWSO
    ZUWTTUWOVVCUWRFFPZSRZUUJUWTUWQUWSVVEUWQUWIVVDUWRSEFFVSVTYFUUJYIYTYOUUGVVEUW
    TTUXBUXKUXJUXFYSDFIYGUSWDUTXIYH $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  ${
    $d A e p q r $.  $d B e p q r $.  $d C e p q r $.  $d D e p q r $.
    $d N e p q r $.  $d b e p q r $.  $d c e p q r $.  $d d e p q r $.
    $( Lemma for ~ btwnconn1 .  Begin back-filling and eliminating
       hypotheses. $)
    btwnconn1lem13 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
       ( C = c \/ D = d ) ) $=
      ( vp vr wcel w3a cv wa cop cbtwn wbr ccgr adantr ad3antrrr ve cee cfv wne
      vq cn wceq wn df-ne wrex simp2rl simp2ll wb simpl1 simprl1 simpl2 simprrl
      btwncom syl13anc simprl2 simprl3 anbi12d syl5ib wi axpasch syl132anc syld
      jca imp simpll1 axsegcon syl122anc reeanv sylanbrc ad2antrr simprl simprr
      simpr simpl simplrl simprrr simpllr simplrr simp1ll simp1lr simpl1r jca32
      3jca simpll2 simpl3l simpl3r btwnconn1lem12 an4s r19.23adv mpd r19.23advv
      syl2an exp32 expr syl5bir orrd ) EUFKZAEUBUCZKZBXCKZLZCXCKZDXCKZGMZXCKZLZ
      HMZXCKZFMZXCKZNZNZNZABUDZBCUDZNZBACOPQBADOPQNZNZDAXIOPQZDXIOCDOZRQZNZCAXL
      OPQZCXLOZYERQZNZNZXIAXNOZPQXIXNOCBORQNZXLYMPQXLXNODBORQNZNZLZNZCXIUGZDXLU
      GZYSUHCXIUDZYRYTCXIUIXRYQUUAYTXRYQUUANZNZUAMZCXIOPQUUDDXLOPQNZUAXCUJZYTXR
      UUBUUFXRUUBCXLAOPQZDXIAOPQZNZUUFUUBYHYDNXRUUIUUBYHYDYQYHUUAYHYJYGYCYPUKSY
      QYDUUAYDYFYKYCYPULSVHXRYHUUGYDUUHXRXBXGXDXMYHUUGUMXBXDXEXQUNZXGXHXJXPXFUO
      ZXBXDXEXQUPZXFXKXMXOUQZCAXLEURUSXRXBXHXDXJYDUUHUMUUJXGXHXJXPXFUTZUULXGXHX
      JXPXFVAZDAXIEURUSVBVCXRXBXMXJXDXGXHUUIUUFVDUUJUUMUUOUULUUKUUNUAXLXIACDEVE
      VFVGVIUUCUUEYTUAXCUUCUUDXCKZUUEYTXRUUPUUBUUEYTXRUUPNZUUBUUENZNZCXIIMZOPQC
      UUTOYIRQNZCXLJMZOPQCUVBOCUUDORQNZNZJXCUJIXCUJZYTUUQUVEUURUUQUVAIXCUJZUVCJ
      XCUJZUVEUUQXBXJXGXGXMUVFXBXDXEXQUUPVJZXRXJUUPUUOSXRXGUUPUUKSZUVIXRXMUUPUU
      MSZIXICCXLEVKVLUUQXBXMXGXGUUPUVGUVHUVJUVIUVIXRUUPVRJXLCCUUDEVKVLUVAUVCIJX
      CXCVMVNSUUSUVDYTIJXCXCUUSUUTXCKZUVBXCKZNZUVDYTUUQUVMUURUVDYTUUQUVMNZUURUV
      DNZNZUVBUUTUEMZOPQUVBUVQOUVBUUTORQNZUEXCUJZYTUVNUVSUVOUVNXBUVKUVLUVLUVKUV
      SXRXBUUPUVMUUJVOUUQUVKUVLVPZUUQUVKUVLVQZUWAUVTUEUUTUVBUVBUUTEVKVLSUVPUVRY
      TUEXCUVPUVQXCKZUVRYTUVNUWBUVOUVRYTUVNUWBNZXFXKXMXOUUPLZNZUVKUWBUVLLZLXSXT
      UUALZYBNZYLYPLZUUEUVAUVCUVRLZNNYTUVOUVRNZUWCXFUWEUWFXRXFUUPUVMUWBXFXQVSTU
      WCXKUWDUUQXKUVMUWBXFXKXPUUPVTVOUWCXMXOUUPXRXMUUPUVMUWBUUMTXRXOUUPUVMUWBXF
      XKXMXOWATXRUUPUVMUWBWBWHVHUWCUVKUWBUVLUUQUVKUVLUWBVTUVNUWBVRUUQUVKUVLUWBW
      CWHWHUWKUWIUUEUWJUWKUWHYLYPUWKUWGYBUWKXSXTUUAUVOXSUVRYQXSUUAUUEUVDXSXTYBY
      LYPWDTSUVOXTUVRYQXTUUAUUEUVDXSXTYBYLYPWETSUVOUUAUVRYQUUAUUEUVDWBSWHUUBYBU
      UEUVDUVRYAYBYLYPUUAWFTVHUURYLUVDUVRYCYLYPUUAUUEWIVOUWKYNYOUUBYNUUEUVDUVRY
      NYOYCYLUUAWJTUUBYOUUEUVDUVRYNYOYCYLUUAWKTVHWHUUBUUEUVDUVRWBUWKUVAUVCUVRUU
      RUVAUVCUVRVTUURUVAUVCUVRWCUVOUVRVRWHWGABCDUUTUVQUVBUUDEFGHWLWQWMWRWNWOWMW
      RWPWOWMWRWNWOWSWTXA $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  ${
    $d A b c d x $.  $d B b c d x $.  $d C b c d x $.  $d D b c d x $.
    $d N b c d x $.
    $( Lemma for ~ btwnconn1 .  Final statement of the theorem when
       ` B =/= C ` . $)
    btwnconn1lem14 $p |- ( (
     ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) /\
       ( ( A =/= B /\ B =/= C ) /\
         ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) ) ->
         ( C Btwn <. A , D >. \/ D Btwn <. A , C >. ) ) $=
      ( vc vd vb vx wcel wa w3a cop cbtwn wbr cv ccgr wrex axsegcon 3jca cn cee
      cfv wne simp1 simp2l simp3r simp3 syl121anc simp3l reeanv sylanbrc adantr
      wo wceq simpl1 simpl2l simprl simpl3l simpl2r syl122anc simprr jca sylibr
      simpl3r weq simplrr simpll simplr simpr btwnconn1lem2 syl2an opeq2 breq2d
      breq1d anbi12d anbi2d biimpac jca32 btwnconn1lem13 syl5 expdimp mpd exp32
      ex an4s r19.23advv orcom simprrl adantl syl5ibrcom simprll orim12d syl5bi
      ) EUAJZAEUBUCZJZBWPJZKZCWPJZDWPJZKZLZABUDBCUDKBACMZNOBADMZNOKKZKZDAFPZMZN
      OZDXHMCDMZQOZKZCAGPZMZNOZCXNMXKQOZKZKZGWPRFWPRZCXENOZDXDNOZUNZXCXTXFXCXMF
      WPRZXRGWPRZXTXCWOWQXAXBYDWOWSXBUEZWOWQWRXBUFZWOWSWTXAUGWOWSXBUHZFADCDESUI
      XCWOWQWTXBYEYFYGWOWSWTXAUJYHGACCDESUIXMXRFGWPWPUKULUMXGXSYCFGWPWPXGXHWPJZ
      XNWPJZKZXSYCXCYKXFXSYCXCYKKZXFXSKZKZCXHUOZDXNUOZUNZYCYNXHAHPZMZNOXHYRMCBM
      QOKZXNAIPZMZNOZXNUUAMZDBMZQOZKZKZIWPRHWPRZYQYNYTHWPRZUUGIWPRZKZUUIYLUULYM
      YLUUJUUKYLWOWQYIWTWRUUJWOWSXBYKUPZWQWRWOXBYKUQZXCYIYJURZWTXAWOWSYKUSZWQWR
      WOXBYKUTZHAXHCBESVAYLWOWQYJXAWRUUKUUMUUNXCYIYJVBWTXAWOWSYKVEZUUQIAXNDBESV
      AVCUMYTUUGHIWPWPUKVDYNUUHYQHIWPWPYNYRWPJZUUAWPJZKZUUHYQYLUVAYMUUHYQYLUVAK
      ZYMUUHKZKIHVFZYQUVBWOWQWRLZWTXAYILZYJUUSUUTLZLXFXSUUHLUVDUVCUVBUVEUVFUVGY
      LUVEUVAYLWOWQWRUUMUUNUUQTUMZYLUVFUVAYLWTXAYIUUPUURUUOTUMZUVBYJUUSUUTXCYIY
      JUVAVGZYLUUSUUTURZYLUUSUUTVBTTUVCXFXSUUHXFXSUUHVHXFXSUUHVIYMUUHVJTABCDEUU
      AHFGVKVLUVBUVCUVDYQUVCUVDKYMYTXNYSNOZXNYRMZUUEQOZKZKZKZUVBYQUVDUVCUVQUVDU
      UHUVPYMUVDUUGUVOYTUVDUUCUVLUUFUVNUVDUUBYSXNNUUAYRAVMVNUVDUUDUVMUUEQUUAYRX
      NVMVOVPVQVQVRUVBUVQYQUVBUVEUVFYJUUSKZKKXFXSUVPLYQUVQUVBUVEUVFUVRUVHUVIUVB
      YJUUSUVJUVKVCVSUVQXFXSUVPXFXSUVPVHXFXSUVPVIYMUVPVJTABCDEHFGVTVLWEWAWBWCWF
      WDWGWCYQYPYOUNYNYCYOYPWHYNYPYAYOYBYNYAYPXPYMXPYLXFXMXPXQWIWJYPXEXOCNDXNAV
      MVNWKYNYBYOXJYMXJYLXFXJXLXRWLWJYOXDXIDNCXHAVMVNWKWMWNWCWFWDWGWC $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  $( Connectitivy law for betweenness.  Theorem 5.1 of [Schwabhauser],
     p. 39-41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
  btwnconn1 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
     ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
     ( ( A =/= B /\ B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ->
       ( C Btwn <. A , D >. \/ D Btwn <. A , C >. ) ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wo wi wceq jca ex breq1 3ad2ant3
    3anbi3d orc syl6bi adantld simpr1 simpl 3simpc adantl btwnconn1lem14 sylan2
    an12s pm2.61ine ) EFGAEHIZGBUOGJCUOGDUOGJKZABLZBACMZNOZBADMZNOZKZCUTNOZDURN
    OZPZUPVBJZVEQBCBCRZVBVEUPVGVBUQUSVCKVEVGVAVCUQUSBCUTNUAUCVCUQVEUSVCVDUDUBUE
    UFBCLZVFVEUPVHVBVEVHVBJZUPUQVHJZUSVAJZJVEVIVJVKVIUQVHVHUQUSVAUGVHVBUHSVBVKV
    HUQUSVAUIUJSABCDEUKULUMTUNT $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  $( Another connectivity law for betweenness.  Theorem 5.2 of [Schwabhauser],
     p. 41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
  btwnconn2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( ( A =/= B /\ B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ->
      ( C Btwn <. B , D >. \/ D Btwn <. B , C >. ) ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wo btwnconn1 wi btwnexch3 ex mpd
    simpr2 anim1i ad2antrr simpr3 simp3r simp3l jca syld3an3 mpand orim12d mpdd
    adantr ) EFGZAEHIZGBUOGJZCUOGZDUOGZJZKZABLZBACMZNOZBADMZNOZKZCVDNOZDVBNOZPZ
    CBDMNOZDBCMNOZPZABCDEQUTVFVIVLRUTVFJZVGVJVHVKVMVGVJVMVGJVCVGJZVJVMVCVGUTVAV
    CVEUBUCUTVNVJRVFVGABCDESUDUATVMVEVHVKUTVAVCVEUEUTVEVHJVKRZVFUNUPUSURUQJVOUT
    URUQUNUPUQURUFUNUPUQURUGUHABDCESUIUMUJUKTUL $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  ${
    $d N p $.  $d A p $.  $d B p $.  $d C p $.  $d D p $.
    $( Inner connectivity law for betweenness.  Theorem 5.3 of [Schwabhauser],
       p. 41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
    btwnconn3 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( ( B Btwn <. A , D >. /\ C Btwn <. A , D >. ) ->
      ( B Btwn <. A , C >. \/ C Btwn <. A , B >. ) ) ) $=
      ( vp wcel wa w3a cop cbtwn wbr wi wb btwncom syl13anc adantr btwnexch3and
      wne mpbid cn cee cfv cv wo simp1 simp3r simp2l btwndiff syl111anc simprlr
      wrex necomd simpl3r simpl2r simpl2l simpr simprrl simprll simpl3l simprrr
      simpl1 3jca ex btwnconn2 syl122anc syld exp3a r19.23adva mpd ) EUAGZAEUBU
      CZGZBVLGZHZCVLGZDVLGZHZIZADFUDZJKLZAVTSZHZFVLULZBADJZKLZCWEKLZHZBACJKLCAB
      JKLUEZMZVSVKVQVMWDVKVOVRUFVKVOVPVQUGVKVMVNVRUHDAEFUIUJVSWCWJFVLVSVTVLGZHZ
      WCWHWIWLWCWHHZVTASZAVTBJKLZAVTCJKLZIZWIWLWMWQWLWMHZWNWOWPWRAVTWLWAWBWHUKU
      MWRABVTJKLZWOWLWMDBAVTEVKVOVRWKVBZVPVQVKVOWKUNZVMVNVKVRWKUOZVMVNVKVRWKUPZ
      VSWKUQZWRWFBDAJZKLZWLWCWFWGURWLWFXFNZWMWLVKVNVMVQXGWTXBXCXABADEOPQTWLWAWB
      WHUSZRWLWSWONZWMWLVKVMVNWKXIWTXCXBXDABVTEOPQTWRACVTJKLZWPWLWMDCAVTEWTXAVP
      VQVKVOWKUTZXCXDWRWGCXEKLZWLWCWFWGVAWLWGXLNZWMWLVKVPVMVQXMWTXKXCXACADEOPQT
      XHRWLXJWPNZWMWLVKVMVPWKXNWTXCXKXDACVTEOPQTVCVDWLVKWKVMVNVPWQWIMWTXDXCXBXK
      VTABCEVEVFVGVHVIVJ $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}


  ${
    $d Q a x $.  $d N a x $.  $d A a x $.  $d B a x $.  $d C a x $.
    $( Generalization of ~ axsegcon .  This time, we generate an endpoint for a
       segment on the ray ` Q A ` congruent to ` B C ` and starting at ` Q ` ,
       as opposed to ~ axsegcon , where the segment starts at ` A `
       (Contributed by Scott Fenton, 14-Oct-2013.) $)
    segcon2 $p |- ( ( N e. NN /\
       ( Q e. ( EE ` N ) /\ A e. ( EE ` N ) ) /\
       ( B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       ( A =/= Q ->
         E. x e. ( EE ` N ) ( ( A Btwn <. Q , x >. \/ x Btwn <. Q , A >. ) /\
           <. Q , x >. Cgr <. B , C >. ) ) ) $=
      ( va wcel wa w3a wne cop cbtwn wbr ccgr wrex syl122anc adantr wi mpd wceq
      cn cee cfv cv simp1 simp2r simp2l axsegcon simpl1 simpl2l simpl3l simpl3r
      wo simpr anass bicomi df-3an simpr1 wb simprl simpl2r cgrcomland cgrdegen
      simpr2r necon3bid mpbird simpr2l btwncomand simpr3l 3jca simprr btwnconn2
      simpr3r jca syl2anbr expr an32s reximdva r19.23adva ex ) FUBHZEFUCUDZHZBW
      CHZIZCWCHZDWCHZIZJZBEKZBEAUEZLZMNWLEBLMNUNZWMCDLONZIZAWCPZWJWKIZEBGUEZLMN
      ZEWSLBELZONZIZGWCPZWQWJXDWKWJWBWEWDWEWDXDWBWFWIUFWBWDWEWIUGZWBWDWEWIUHZXE
      XFGBEBEFUIQRWRXCWQGWCWJWSWCHZWKXCWQSWJXGIZWKXCWQXHWKXCIZIZEWSWLLMNZWOIZAW
      CPZWQXHXMXIXHWBXGWDWGWHXMWBWFWIXGUJWJXGUOWDWEWBWIXGUKWGWHWBWFXGULWGWHWBWF
      XGUMAWSECDFUIQRXJXLWPAWCXHWLWCHZXIXLWPSXHXNIZXIXLWPXOWJXGXNIZIZWKXCXLJZWP
      XIXLIXOXQWJXGXNUPUQWKXCXLURXQXRIZWNWOXSWSEKZEWSBLMNZXKJZWNXSXTYAXKXSXTWKX
      QWKXCXLUSXSWSEBEXSWSELXAONZWSEUABEUAUTZXQXREWSBEFWBWFWIXPUJZWDWEWBWIXPUKZ
      WJXGXNVAZWDWEWBWIXPVBZYFWTXBWKXLXQVEVCXQYCYDSZXRXQWBXGWDWEWDYIYEYGYFYHYFW
      SEBEFVDQRTVFVGXQXREBWSFYEYFYHYGWTXBWKXLXQVHVIXKWOWKXCXQVJVKXQYBWNSZXRXQWB
      XGWDWEXNYJYEYGYFYHWJXGXNVLWSEBWLFVMQRTXKWOWKXCXQVNVOVPVQVRVSTVQVRVTTWA $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

