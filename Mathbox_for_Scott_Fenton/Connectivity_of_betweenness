$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Connectivity of betweenness
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Lemma for ~ btwnconn1 .  The next several lemmas introduce various
     properties of hypothetical points that end up eliminating alternatives to
     connectivity.  We begin by showing a congruence property of those
     hypothetical points. $)
  btwnconn1lem1 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ X e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , X >. /\ <. d , X >. Cgr <. D , B >. ) ) ) ) ->
     <. B , c >. Cgr <. X , C >. ) $=
    ( wcel w3a cv wne wa cop cbtwn wbr ccgr adantl btwnexch3and cee cfv simp1rr
    cn simp11 simp13 simp22 simp23 simp33 simp31 simp21 simp2ll simp2rl simp3rl
    simp12 btwncomand simp3rr cgrcomand simp2lr simp2rr cgrcomland cgrextendand
    cgrcomlrand cgrtr3and ) EUDJZAEUAUBZJZBVFJZKZCVFJZDVFJZHLZVFJZKZILZVFJZGLZV
    FJZFVFJZKZKZABMBCMNZBACOPQZBADOPQZNNZDAVLOPQZDVLOCDOZRQZNZCAVOOPQZCVOOWGRQZ
    NZNZVLAVQOPQVLVQOCBORQNZVOAFOPQZVOFODBORQZNNZKZBDVLFVOCEVEVGVHVNVTUEZVEVGVH
    VNVTUFZVIVJVKVMVTUGZVIVJVKVMVTUHZVIVNVPVRVSUIZVIVNVPVRVSUJZVIVJVKVMVTUKZWAW
    RABDVLEWSVEVGVHVNVTUOZWTXAXBWRWDWAWCWDWBWMWQUCSWRWFWAWFWHWLWEWQULSTWAWRVOCF
    EWSXDXEXCWAWRACVOFEWSXFXEXDXCWRWJWAWJWKWIWEWQUMSWRWOWAWOWPWNWEWMUNSTUPWAWRD
    BVOFEWSXAWTXDXCWAWRVOFDBEWSXDXCXAWTWRWPWAWOWPWNWEWMUQSURVCWAWRDVLVOCCDEWSXA
    XBXDXEXEXAWRWHWAWFWHWLWEWQUSSWAWRCVOCDEWSXEXDXEXAWRWKWAWJWKWIWEWQUTSVAVDVB
    $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we show that two of the hypotheticals we
     introduced in the first lemma are identical. $)
  btwnconn1lem2 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ X e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , X >. /\ <. d , X >. Cgr <. D , B >. ) ) ) ) ->
     X = b ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr adantl jca adantr cn cee cfv wne simp11
    simp1ll simp12 simp13 simp21 simp33 simp1rl simp2rl simp3rl simp31 btwnexch
    wi syl122anc mpd btwnexchand axcgrrflx syl3anc simp23 simp32 simp1rr simp22
    wceq simp2ll simp3ll btwnexch3and wb btwncom syl13anc btwnconn1lem1 simp3lr
    mpbid cgrextendand 3jca segconeq syl133anc ) EUAJZAEUBUCZJZBWAJZKZCWAJZDWAJ
    ZHLZWAJZKZILZWAJZGLZWAJZFWAJZKZKZABUDZBCUDZMZBACNOPZBADNOPZMZMZDAWGNZOPZDWG
    NCDNZQPZMZCAWJNOPZCWJNXFQPZMZMZWGAWLNZOPZWGWLNCBNQPZMZWJAFNZOPZWJFNDBNQPZMZ
    MZKZMZWQBXQOPZBFNZFBNZQPZMZBXMOPZBWLNYFQPZMZKZFWLVFZYCWQYHYKYBWQWPWQWRXBXLY
    AUFRYCYDYGWPYBABCFEVTWBWCWIWOUEZVTWBWCWIWOUGZVTWBWCWIWOUHZWDWEWFWHWOUIZWDWI
    WKWMWNUJZYBWTWPWTXAWSXLYAUKRZYCXIXRMZCXQOPZYBYTWPYBXIXRXIXJXHXCYAULXRXSXPXC
    XLUMSRWPYTUUAUPZYBWPVTWBWEWKWNUUBYNYOYQWDWIWKWMWNUNYRACWJFEUOUQTURZUSWPYGYB
    WPVTWCWNYGYNYPYRBFEUTVATSYCYIYJWPYBABWGWLEYNYOYPWDWEWFWHWOVBZWDWIWKWMWNVCZY
    CXAXEMZBXDOPZYBUUFWPYBXAXEWTXAWSXLYAVDXEXGXKXCYAVGSRWPUUFUUGUPZYBWPVTWBWCWF
    WHUUHYNYOYPWDWEWFWHWOVEUUDABDWGEUOUQTURZYBXNWPXNXOXTXCXLVHRZUSWPYBBWGWLFCBE
    YNYPUUDUUEYRYQYPWPYBABWGWLEYNYOYPUUDUUEUUIUUJVIYCCYEOPZCYFOPZWPYBABCFEYNYOY
    PYQYRYSUUCVIWPUUKUULVJZYBWPVTWEWCWNUUMYNYQYPYRCBFEVKVLTVOABCDEFGHIVMYBXOWPX
    NXOXTXCXLVNRVPSVQWPYLYMUPZYBWPVTWCWNWCWBWNWMUUNYNYPYRYPYOYRUUEBFBAEFWLVRVST
    UR $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Establish the next congruence in the series. $)
  btwnconn1lem3 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
     <. B , d >. Cgr <. b , D >. ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr adantl syl122anc adantr wb cn simp11 wi
    cee cfv simp13 simp21 simp3l simp3r simp23 simp22 simp1rl simp2rl btwnexch3
    wne jca simp12 mpd simp2ll simp3ll btwncom syl13anc simp3lr cgrcomlr cgrcom
    mpbid bitrd simp2rr simp2lr cgrcomland cgrtr3 syl133anc mp2and cgrextendand
    ) EUAIZAEUDUEZIZBVPIZJZCVPIZDVPIZGKZVPIZJZHKZVPIZFKZVPIZLZJZABUOBCUOLZBACMN
    OZBADMNOZLLZDAWBMNOZDWBMCDMZPOZLZCAWEMNOZCWEMZWPPOZLZLZWBAWGMZNOZWBWGMCBMPO
    ZLWEXDNOWEWGMDBMPOLZLZJZBCWEWGWBDEVOVQVRWDWIUBZVOVQVRWDWIUFZVSVTWAWCWIUGZVS
    WDWFWHUHZVSWDWFWHUIZVSVTWAWCWIUJZVSVTWAWCWIUKZWJXILZWLWSLZCBWEMNOZXIXRWJXIW
    LWSWLWMWKXCXHULWSXAWRWNXHUMUPQWJXRXSUCZXIWJVOVQVRVTWFXTXJVOVQVRWDWIUQZXKXLX
    MABCWEEUNRSURXQWBDWGMNOZWBWGDMNOZXQWOXELZYBXIYDWJXIWOXEWOWQXBWNXHUSXEXFXGWN
    XCUTUPQWJYDYBUCZXIWJVOVQWAWCWHYEXJYAXPXOXNADWBWGEUNRSURWJYBYCTZXIWJVOWCWAWH
    YFXJXOXPXNWBDWGEVAVBSVFXQXFBCMZWGWBMZPOZXIXFWJXEXFXGWNXCVCQWJXFYITXIWJXFYHY
    GPOZYIWJVOWCWHVTVRXFYJTXJXOXNXLXKWBWGCBEVDRWJVOWHWCVRVTYJYITXJXNXOXKXLWGWBB
    CEVERVGSVFXQXAWBDMZWPPOZWTYKPOZXIXAWJWSXAWRWNXHVHQWJXIDWBCDEXJXPXOXLXPXIWQW
    JWOWQXBWNXHVIQVJWJXAYLLYMUCZXIWJVOVTWFWCWAVTWAYNXJXLXMXOXPXLXPCWEWBDCDEVKVL
    SVMVN $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Assuming ` C =/= c ` , we now attempt to force
     ` D = d ` from here out via a series of congruences. $)
  btwnconn1lem4 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) /\
         ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
             ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
           ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
             ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
           ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
             ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
     <. d , c >. Cgr <. D , C >. ) $=
    ( wcel w3a cv wa wne cop cbtwn wbr ccgr adantl adantr wb cn cee cfv simp1rl
    ccgr3 simp2rl jca wi simp11 simp12 simp13 simp21 simp3l btwnexch3 syl122anc
    mpd simp3lr simp23 simp3r cgrcomlr cgrcom bitrd 3simpa anim1i btwnconn1lem3
    mpbid simp2rr simp22 simp2lr cgrcomland cgrtr3 syl133anc mp2and 3jca brcgr3
    syl3anr1 mpbird simpl simpr 3anim3i 3anim1i btwnconn1lem1 axcgrrflx syl3anc
    syl2an simp1l2 cofs brofs2 anbi1d 5segofs sylbird syl333anc ) EUAIZAEUBUCZI
    ZBWNIZJZCWNIZDWNIZGKZWNIZJZHKZWNIZFKZWNIZLZJZABMZBCMZCWTMZJZBACNOPZBADNOPZL
    ZLZDAWTNOPZDWTNCDNZQPZLZCAXCNOPZCXCNZXRQPZLZLZWTAXENZOPZWTXENCBNQPZLXCYFOPX
    CXENDBNQPLZLZJZLZCBXCNZOPZBYBNXEWTDNZNUEPZBWTNXECNQPZCWTNWTCNQPZLZJZXJXCWTN
    ZDCNZQPZYLYNYPYSYLXMYALZYNYKUUDXHYKXMYAXMXNXLYEYJUDYAYCXTXPYJUFUGRXHUUDYNUH
    ZYKXHWMWOWPWRXDUUEWMWOWPXBXGUIZWMWOWPXBXGUJWMWOWPXBXGUKZWQWRWSXAXGULZWQXBXD
    XFUMZABCXCEUNUOSUPYLYPBCNZXEWTNZQPZYMXEDNQPZYBYOQPZJZYLUULUUMUUNYLYHUULYKYH
    XHYGYHYIXPYEUQRXHYHUULTYKXHYHUUKUUJQPZUULXHWMXAXFWRWPYHUUPTUUFWQWRWSXAXGURZ
    WQXBXDXFUSZUUHUUGWTXECBEUTUOXHWMXFXAWPWRUUPUULTUUFUURUUQUUGUUHXEWTBCEVAUOVB
    SVFXPXIXJLZXOLZXHYEYJUUMXLUUSXOXIXJXKVCVDZABCDEFGHVEVPYLYCYOXRQPZUUNYKYCXHY
    AYCXTXPYJVGRXHYKDWTCDEUUFWQWRWSXAXGVHZUUQUUHUVCYKXSXHXQXSYDXPYJVIRVJXHYCUVB
    LUUNUHZYKXHWMWRXDXAWSWRWSUVDUUFUUHUUIUUQUVCUUHUVCCXCWTDCDEVKVLSVMVNXHYPUUOT
    ZYKXHWMWPWRXDXFXAWSUVEUUFUUGUUHUUIUURUUQUVCBCXCXEWTDEVOVLSVQYLYQYRXHWQXBXDX
    FXFJZJUUTYEYJJYQYKXGUVFWQXBXGXDXFXFXDXFVRXDXFVSZUVGVNVTXPUUTYEYJUVAWAABCDEX
    EFGHWBWEXHYRYKXHWMWRXAYRUUFUUHUUQCWTEWCWDSUGVNYKXJXHXIXJXKXOYEYJWFRXHYTXJLZ
    UUCUHZYKXHWMWPWRXDXAXFXAWSWRUVIUUFUUGUUHUUIUUQUURUUQUVCUUHWMWPWRJXDXAXFJXAW
    SWRJJZUVHUUJUUANUUKUUBNWGPZXJLUUCUVJUVKYTXJBCXCWTXEWTDCEWHWIBCXCWTXEWTDCEWJ
    WKWLSVM $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we introduce ` E ` , the intersection of
     ` C c ` and ` D d ` .  We begin by showing that it is the midpoint of
     ` C ` and ` c ` $)
  btwnconn1lem5 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
         <. E , C >. Cgr <. E , c >. ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr adantr wb cn cee cfv simprrr simp11
    ccgr3 simp22 simp33 simp31 cgr3rflx syl13anc simp2lr ad2antrl simp23 simp21
    cgrcomr cgrcom bitrd mpbid simp2rr cgrcomlrand 3simpa 3anim3i btwnconn1lem4
    syl122anc simpl syl2an cgrtr3 syl133anc mp2and jca 3jca cifs brifs2 sylbird
    wi ifscgr syl333anc mpd ) FUAJZAFUBUCZJZBWAJZKZCWAJZDWAJZHLZWAJZKZILZWAJZGL
    ZWAJZEWAJZKZKZABMBCMCWGMKBACNOPBADNOPQQZDAWGNOPZDWGNZCDNZRPZQZCAWJNOPZCWJNW
    TRPZQZQWGAWLNZOPWGWLNCBNRPQWJXFOPWJWLNDBNRPQQZKZECWGNOPZEDWJNOPZQZQZQZXJDEW
    JNNZXNUFPZDCNZWSRPZWJCNZWJWGNZRPZQZKZECNEWGNRPZXMXJXOYAWPXHXIXJUDWPXOXLWPVT
    WFWNWKXOVTWBWCWIWOUEZWDWEWFWHWOUGZWDWIWKWMWNUHZWDWIWKWMWNUIZDEWJFUJUKSXMXQX
    TXMXAXQXHXAWPXKWRXAXEWQXGULUMWPXAXQTXLWPXAWSXPRPZXQWPVTWFWHWEWFXAYHTYDYEWDW
    EWFWHWOUNZWDWEWFWHWOUOZYEDWGCDFUPVEWPVTWFWHWFWEYHXQTYDYEYIYEYJDWGDCFUQVEURS
    USXMXRXPRPZXSXPRPZXTWPXLCWJCDFYDYJYGYJYEXHXDWPXKXCXDXBWQXGUTUMVAWPWDWIWKWMQ
    ZKXHYLXLWOYMWDWIWKWMWNVBVCXHXKVFABCDFGHIVDVGWPYKYLQXTVPZXLWPVTWKWEWKWHWFWEY
    NYDYGYJYGYIYEYJWJCWJWGDCFVHVISVJVKVLWPYBYCVPZXLWPVTWFWNWKWEWFWNWKWHYOYDYEYF
    YGYJYEYFYGYIVTWFWNKWKWEWFKWNWKWHKKYBDENZXRNYPXSNVMPYCDEWJCDEWJWGFVNDEWJCDEW
    JWGFVQVOVRSVS $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Next, we show that ` E ` is the midpoint of ` D `
     and ` d ` $)
  btwnconn1lem6 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
         <. E , D >. Cgr <. E , d >. ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr jca adantr cn cee cfv simp11 simp21
    simprrl simp23 cgrrflx syl3anc simp33 simp2rr ad2antrl cgrcomand cgrcomrand
    simp31 simp22 simp2lr 3simpa simpl btwnconn1lem4 syl2an wi cgrtr3 syl133anc
    3anim3i mp2and cgrcomlrand 3jca cifs brifs ifscgr sylbird syl333anc mpd ) F
    UAJZAFUBUCZJZBVPJZKZCVPJZDVPJZHLZVPJZKZILZVPJZGLZVPJZEVPJZKZKZABMBCMCWBMKBA
    CNOPBADNOPQQZDAWBNOPZDWBNZCDNZRPZQZCAWENOPZCWENZWORPZQZQWBAWGNZOPWBWGNCBNRP
    QWEXBOPWEWGNDBNRPQQZKZECWBNZOPZEDWENOPZQZQZQZXFXFQZXEXERPZEWBNZXMRPZQZWOWSR
    PZWBDNZWBWENZRPZQZKZEDNEWENRPZXJXKXOXTXJXFXFWKXDXFXGUFZYCSWKXOXIWKXLXNWKVOV
    TWCXLVOVQVRWDWJUDZVSVTWAWCWJUEZVSVTWAWCWJUGZCWBFUHUIWKVOWIWCXNYDVSWDWFWHWIU
    JZYFEWBFUHUISTXJXPXSWKXICWECDFYDYEVSWDWFWHWIUOZYEVSVTWAWCWJUPZXDWTWKXHWRWTW
    QWLXCUKULUMWKXIDWBWEWBFYDYIYFYHYFXJWNDCNZRPZWEWBNZYJRPZWNYLRPZWKXIDWBCDFYDY
    IYFYEYIXDWPWKXHWMWPXAWLXCUQULUNWKVSWDWFWHQZKXDYMXIWJYOVSWDWFWHWIURVEXDXHUSA
    BCDFGHIUTVAWKYKYMQYNVBZXIWKVOWAWCWFWCWAVTYPYDYIYFYHYFYIYEDWBWEWBDCFVCVDTVFV
    GSVHWKYAYBVBZXIWKVOVTWIWCWAVTWIWCWFYQYDYEYGYFYIYEYGYFYHVOVTWIKWCWAVTKWIWCWF
    KKYACENZXQNYRXRNVIPYBCEWBDCEWBWEFVJCEWBDCEWBWEFVKVLVMTVN $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Under our assumptions, ` C ` and ` d ` are
     distinct. $)
  btwnconn1lem7 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
         ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) ) ) ->
        C =/= d ) $=
    ( wcel w3a cv wne cop cbtwn wbr wa ccgr wi syl5 cee simp1l3 simp2rr simp2lr
    cfv adantr 3jca simp11 simp21 simp22 simp23 simp31 simpr1 wceq opeq2 breq1d
    cn 3anbi2d biimparc simp2 simp1 simp2l simp2r cgrid2 syl13anc opeq1 breq12d
    imp simp3l axcgrid expdimp 3ad2antr3 mpd ex necon3d syl122anc ) FUQJZAFUAUE
    ZJZBVRJZKZCVRJZDVRJZHLZVRJZKZILZVRJZGLZVRJZEVRJZKZKZABMZBCMZCWDMZKBACNOPBAD
    NOPQZQZDAWDNOPZDWDNZCDNZRPZQZCAWGNOPZCWGNZXARPZQZQZWDAWINZOPWDWINCBNRPQWGXI
    OPWGWINDBNRPQQZKZECWDNZOPEDWGNOPQZQZCWGMZXNWPXFXBKZWMXOXNWPXFXBXKWPXMWNWOWP
    WQXHXJUBUFXKXFXMXDXFXCWRXJUCUFXKXBXMWSXBXGWRXJUDUFUGWMVQWBWCWEWHXPXOSVQVSVT
    WFWLUHWAWBWCWEWLUIWAWBWCWEWLUJWAWBWCWEWLUKWAWFWHWJWKULVQWBWCQZWEWHQZKZXPXOX
    SXPQZWPXOXSWPXFXBUMXTCWGCWDXSXPCWGUNZCWDUNZXPYAQWPCCNZXARPZXBKZXSYBYAYEXPYA
    YDXFWPXBYAYCXEXARCWGCUOUPURUSXSYEYBXSYEQCDUNZYBXSYEYFYEYDXSYFWPYDXBUTXSVQWB
    WBWCYDYFSVQXQXRVAZVQWBWCXRVBZYHVQWBWCXRVCCCDFVDVETVHXSWPXBYFYBSYDXSXBYFYBXB
    YFQXLYCRPZXSYBYFYIXBYFXLWTYCXARCDWDVFCDCUOVGUSXSVQWBWEWBYIYBSYGYHVQXQWEWHVI
    YHCWDCFVJVETVKVLVMVNTVKVOVMVNVPTVH $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we introduce the last three points used in
     the construction: ` P ` , ` Q ` , and ` R ` will turn out to be equal
     further down, and will provide us with the key to the final statement.  We
     begin by establishing congruence of ` R P ` and ` E d ` $)
  btwnconn1lem8 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. R , P >. Cgr <. E , d >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr adantr cn cee cfv cv ccgr3 simpr2l simpr1r
    wne ad2antll wb simp11 simp2l1 simp31 cgrcomlr syl122anc cgrcom bitrd mpbid
    simp2r1 simp33 simp2r3 simpr1l simp2l3 btwncom syl13anc simprll wi btwnintr
    adantl mp2and simpr2r cgrextendand 3jca brcgr3 syl133anc mpbird syl3anc jca
    axcgrrflx simp1 simp2l simp2r simpl simprl btwnconn1lem7 syl2an necomd cofs
    brofs2 anbi1d 5segofs sylbird syl333anc ) IUAMZAIUBUCZMZBWOMZNZCWOMZDWOMZKU
    DZWOMZNZLUDZWOMZJUDZWOMZHWOMZNZOZEWOMZFWOMZGWOMZNZNZABUHBCUHCXAUHNBACPQRBAD
    PQROODAXAPQRDXAPCDPZSROCAXDPQRCXDPZXPSROOXAAXFPZQRXAXFPCBPSROXDXRQRXDXFPDBP
    SROONZHCXAPQRZHDXDPQRZOZCXAEPQRZCEPXQSRZOZCXDGPZQRZCGPZCHPZSRZOZGEFPQRGFPGE
    PZSROZNZOZOZOZYGXDYHPEYIPUERZXDEPEXDPSRZYDOZNZXDCUHZYLHXDPZSRZYQYGYRYTYOYGX
    OXSYGYJYEYMYBUFUIZYQYRXDCPZECPZSRZYFEHPZSRZYJNZYQUUHUUJYJYQYDUUHYOYDXOXSYCY
    DYKYMYBUGUIZXOYDUUHUJYPXOYDUUGUUFSRZUUHXOWNWSXKWSXEYDUUMUJWNWPWQXJXNUKZWSWT
    XBXIWRXNULZWRXJXKXLXMUMZUUOXEXGXHXCWRXNUSZCECXDIUNUOXOWNXKWSXEWSUUMUUHUJUUN
    UUPUUOUUQUUOECXDCIUPUOUQTURZXOYPXDCGECHIUUNUUQUUOWRXJXKXLXMUTZUUPUUOXEXGXHX
    CWRXNVAZUUEYQCEXAPQRZXTCUUIQRZYQYCUVAYOYCXOXSYCYDYKYMYBVBUIXOYCUVAUJZYPXOWN
    WSXBXKUVCUUNUUOWSWTXBXIWRXNVCZUUPCXAEIVDVETURYPXTXOXSXTYAYNVFVIXOUVAXTOUVBV
    GZYPXOWNXKWSXHXBUVEUUNUUPUUOUUTUVDECHXAIVHUOTVJUURYOYJXOXSYGYJYEYMYBVKUIZVL
    UVFVMXOYRUUKUJZYPXOWNXEWSXMXKWSXHUVGUUNUUQUUOUUSUUPUUOUUTXDCGECHIVNVOTVPYQY
    SYDXOYSYPXOWNXEXKYSUUNUUQUUPXDEIVSVQTUULVRVMYQCXDXOWRXCXINXSYBOCXDUHYPXOWRX
    CXIWRXJXNVTWRXCXIXNWAWRXCXIXNWBVMYPXSYBXSYOWCXSYBYNWDVRABCDHIJKLWEWFWGXOUUA
    UUBOZUUDVGZYPXOWNXEWSXMXKXKWSXHXEUVIUUNUUQUUOUUSUUPUUPUUOUUTUUQWNXEWSNXMXKX
    KNWSXHXENNZUVHUUFYLPUUGUUCPWHRZUUBOUUDUVJUVKUUAUUBXDCGEECHXDIWIWJXDCGEECHXD
    IWKWLWMTVJ $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, a quick use of transitivity to establish
     congruence on ` R Q ` and ` E D ` $)
  btwnconn1lem9 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. R , Q >. Cgr <. E , D >. ) $=
    ( wcel w3a cv wa cop cbtwn wbr ccgr cee cfv wne simp11 simp33 simp32 simp31
    simp2r3 simp2r1 simpr3r ad2antll btwnconn1lem8 cgrtrand simp1 simp2l simp2r
    cn simpl simprl jca btwnconn1lem6 syl2an wi simp2l2 cgrtr3 syl133anc adantr
    3jca mp2and ) IUQMZAIUAUBZMZBVKMZNZCVKMZDVKMZKOZVKMZNZLOZVKMZJOZVKMZHVKMZNZ
    PZEVKMZFVKMZGVKMZNZNZABUCBCUCCVQUCNBACQRSBADQRSPPDAVQQRSDVQQCDQZTSPCAVTQRSC
    VTQZWLTSPPVQAWBQZRSVQWBQCBQTSPVTWNRSVTWBQDBQTSPPNZHCVQQRSHDVTQRSPZCVQEQRSCE
    QWMTSPZCVTGQRSCGQCHQTSPZGEFQRSZGFQZGEQTSZPNZPZPZPWTHVTQZTSZHDQZXETSZWTXGTSZ
    WKXDGFGEHVTIVJVLVMWFWJUDZVNWFWGWHWIUEZVNWFWGWHWIUFZXKVNWFWGWHWIUGWAWCWDVSVN
    WJUHZWAWCWDVSVNWJUIZXCXAWKWOWSXAWQWRWPUJUKABCDEFGHIJKLULUMWKVNVSWENWOWPPXHX
    DWKVNVSWEVNWFWJUNVNVSWEWJUOVNVSWEWJUPVHXDWOWPWOXCURWOWPXBUSUTABCDHIJKLVAVBW
    KXFXHPXIVCZXDWKVJWIWHWDVPWDWAXOXJXKXLXMVOVPVRWEVNWJVDXMXNGFHDHVTIVEVFVGVI
    $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now we establish a congruence that will give us
     ` D = d ` when we compute ` P = Q ` later on. $)
  btwnconn1lem10 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. d , D >. Cgr <. P , Q >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wb cn cee cfv cv wne simp11 simp2r1 simp31
    simp2r3 simp2l2 simp33 simp32 simprlr adantl btwncom syl13anc mpbid simpr3l
    adantr ad2antll btwnconn1lem8 cgrcomlr syl122anc cgrcom bitrd btwnconn1lem9
    cgrcomand cgrextendand ) IUAMZAIUBUCZMZBVJMZNZCVJMZDVJMZKUDZVJMZNZLUDZVJMZJ
    UDZVJMZHVJMZNZOZEVJMZFVJMZGVJMZNZNZABUEBCUECVPUENBACPQRBADPQROODAVPPQRDVPPC
    DPZSROCAVSPQRCVSPZWKSROOVPAWAPZQRVPWAPCBPSROVSWMQRVSWAPDBPSROONZHCVPPQRZHDV
    SPQRZOZCVPEPQRCEPWLSROZCVSGPQRCGPCHPSROZGEFPQRZGFPGEPZSRZONZOZOZVSHDEGFIVIV
    KVLWEWIUFZVTWBWCVRVMWIUGZVTWBWCVRVMWIUIZVNVOVQWDVMWIUJZVMWEWFWGWHUHZVMWEWFW
    GWHUKZVMWEWFWGWHULZWJXEOZWPHVSDPQRZXEWPWJWNWOWPXCUMUNWJWPXNTZXEWJVIWCVOVTXO
    XFXHXIXGHDVSIUOUPUSUQXDWTWJWNWTXBWRWSWQURUTXMXAHVSPSRZVSHPZEGPZSRZABCDEFGHI
    JKLVAWJXPXSTXEWJXPXRXQSRZXSWJVIWHWFWCVTXPXTTXFXKXJXHXGGEHVSIVBVCWJVIWFWHVTW
    CXTXSTXFXJXKXGXHEGVSHIVDVCVEUSUQWJXEGFHDIXFXKXLXHXIABCDEFGHIJKLVFVGVH $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Now, we establish that ` D ` and ` Q ` are
     equidistant from ` C ` $)
  btwnconn1lem11 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       <. D , C >. Cgr <. Q , C >. ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wb cee cfv wne btwnconn1lem8 btwnconn1lem9
    cn cv wceq btwnconn1lem10 adantr wi simpr3r adantl simpr2r jca opeq2 breq2d
    anbi2d 3anbi12d anbi12d biimpar simpr1 simp11 simp33 simp31 simp2r1 axcgrid
    3jca opeq1 syl5 breq12d breq1d biimpac simpll simp32 simprlr simpr3 simp2l2
    syl13anc imp opeq2d simp2l1 cgrcomlr syl122anc cgrcom bitrd mpbid ex anbi1d
    3anbi23d imbi12d syl5ibcom com23 mpdd exp3a exp4d imp31 mpd simp2r3 btwncom
    ccgr3 cgrcomand brcgr3 syl133anc mpbird simpr1r ad2antll simpr cofs 5segofs
    brofs2 sylbird syl333anc ad2antrr mp2and pm2.61dane ) IUFMZAIUAUBZMZBXRMZNZ
    CXRMZDXRMZKUGZXRMZNZLUGZXRMZJUGZXRMZHXRMZNZOZEXRMZFXRMZGXRMZNZNZABUCBCUCCYD
    UCNBACPQRBADPQROODAYDPQRDYDPCDPZSROCAYGPQRCYGPZYSSROOYDAYIPZQRYDYIPCBPSROYG
    UUAQRYGYIPDBPSROONZHCYDPQRZHDYGPQRZOZCYDEPQRZCEPZYTSRZOZCYGGPQRZCGPZCHPZSRZ
    OZGEFPZQRZGFPZGEPZSRZOZNZOZOZOZDCPZFCPZSRZYGHUVDYGHUHZOUURHYGPZSRZUUQHDPZSR
    ZYGDPZUUOSRZNZUVGUVDUVOUVHUVDUVJUVLUVNABCDEFGHIJKLUDZABCDEFGHIJKLUEZABCDEFG
    HIJKLUIZVHUJYRUVCUVHUVOUVGUKZUVCUUSUUMOZYRUVHUVSUKUVCUUSUUMUVBUUSUUBUUPUUSU
    UIUUNUUEULUMUVBUUMUUBUUJUUMUUIUUTUUEUNZUMUOYRUVHUVTUVSYRUVHUVTUVOUVGUVHUVTU
    VOOZOUUSUUKYTSRZOZUURYGYGPZSRZUUQUVMSRZUVNNZOZYRUVGUVHUWIUWBUVHUWDUVTUWHUVO
    UVHUWCUUMUUSUVHYTUULUUKSYGHCUPUQURUVHUWFUVJUWGUVLUVNUVHUWEUVIUURSYGHYGVIUQU
    VHUVMUVKUUQSYGHDVIUQUSUTVAYRUWIGEUHZUVGUWIUWFYRUWJUWDUWFUWGUVNVBYRXQYPYNYHU
    WFUWJUKXQXSXTYMYQVCZYAYMYNYOYPVDZYAYMYNYOYPVEZYHYJYKYFYAYQVFZGEYGIVGVSVJYRU
    WIUWJUVGUWIUWJOUUOEEPZSRZUUHOZUWOUWESRZUUOUVMSRZUVNNZOZYRUVGUWJUWIUXAUWJUWD
    UWQUWHUWTUWJUUSUWPUWCUUHUWJUUQUUOUURUWOSGEFVIZGEEVIZVKUWJUUKUUGYTSGECUPVLUT
    UWJUWFUWRUWGUWSUVNUWJUURUWOUWESUXCVLUWJUUQUUOUVMSUXBVLUSUTVMYRUXAEFUHZUVGUX
    AUWPYRUXDUWPUUHUWTVNYRXQYNYOYNUWPUXDUKUWKUWMYAYMYNYOYPVOZUWMEFEIVGVSVJYRUXD
    UXAUVGYRUWOUWOSRZUUHOZUWRUWOUVMSRZUVMUWOSRZNZOZUVEECPZSRZUKUXDUXAUVGUKYRUXK
    UXMYRUXKOZUUHUXMYRUXFUUHUXJVPUXNUUHUUGYSSRZUXMUXNYTYSUUGSUXNYGDCYRUXKYGDUHZ
    UXKUXIYRUXPUXGUWRUXHUXIVQYRXQYHYCYNUXIUXPUKUWKUWNYBYCYEYLYAYQVRZUWMYGDEIVGV
    SVJVTWAUQYRUXOUXMTUXKYRUXOUXLUVESRZUXMYRXQYBYNYBYCUXOUXRTUWKYBYCYEYLYAYQWBZ
    UWMUXSUXQCECDIWCWDYRXQYNYBYCYBUXRUXMTUWKUWMUXSUXQUXSECDCIWEWDWFUJWFWGWHUXDU
    XKUXAUXMUVGUXDUXGUWQUXJUWTUXDUXFUWPUUHUXDUWOUUOUWOSEFEUPZVLWIUXDUXHUWSUXIUV
    NUWRUXDUWOUUOUVMSUXTVLUXDUWOUUOUVMSUXTUQWJUTUXDUXLUVFUVESEFCVIUQWKWLWMWNVJW
    OWNVJWPWMVJWQWRUVDYGHUCZOHUVMQRZYGUVKPEUUQPXARZYGCPZUXLSRZHCPZGCPZSRZOZNZUY
    AUVGUVDUYJUYAUVDUYBUYCUYIUVDUUDUYBUVCUUDYRUUBUUCUUDUVAVPUMYRUUDUYBTZUVCYRXQ
    YKYCYHUYKUWKYHYJYKYFYAYQWSZUXQUWNHDYGIWTVSUJWGUVDUYCYGHPZEGPZSRZUVNUVKUUQSR
    ZNZUVDUYOUVNUYPUVDUVJUYOUVPYRUVJUYOTUVCYRUVJUYNUYMSRZUYOYRXQYPYNYKYHUVJUYRT
    UWKUWLUWMUYLUWNGEHYGIWCWDYRXQYNYPYHYKUYRUYOTUWKUWMUWLUWNUYLEGYGHIWEWDWFUJWG
    UVRYRUVCGFHDIUWKUWLUXEUYLUXQUVQXBVHYRUYCUYQTZUVCYRXQYHYKYCYNYPYOUYSUWKUWNUY
    LUXQUWMUWLUXEYGHDEGFIXCXDUJXEUVDUYEUYHUVDUUHUYEUVBUUHYRUUBUUFUUHUUNUUTUUEXF
    XGYRUUHUYETUVCYRUUHUXLUYDSRZUYEYRXQYBYNYBYHUUHUYTTUWKUXSUWMUXSUWNCECYGIWCWD
    YRXQYNYBYHYBUYTUYETUWKUWMUXSUWNUXSECYGCIWEWDWFUJWGUVDUUMUYHUVBUUMYRUUBUWAXG
    YRUUMUYHTUVCYRUUMUYGUYFSRZUYHYRXQYBYPYBYKUUMVUATUWKUXSUWLUXSUYLCGCHIWCWDYRX
    QYPYBYKYBVUAUYHTUWKUWLUXSUYLUXSGCHCIWEWDWFUJWGUOVHUJUVDUYAXHYRUYJUYAOZUVGUK
    ZUVCUYAYRXQYHYKYCYBYNYPYOYBVUCUWKUWNUYLUXQUXSUWMUWLUXEUXSXQYHYKNYCYBYNNYPYO
    YBNNZVUBUYMUVEPUYNUVFPXIRZUYAOUVGVUDVUEUYJUYAYGHDCEGFCIXKWIYGHDCEGFCIXJXLXM
    XNXOXP $.
    $( [14-Oct-2013] $) $( [8-Oct-2013] $)

  $( Lemma for ~ btwnconn1 .  Using a long string of invocations of ~ linecgr ,
     we show that ` D = d ` . $)
  btwnconn1lem12 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) /\
         ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) ) /\
         ( ( ( ( A =/= B /\ B =/= C /\ C =/= c ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) /\
           ( ( E Btwn <. C , c >. /\ E Btwn <. D , d >. ) /\
             ( ( C Btwn <. c , P >. /\ <. C , P >. Cgr <. C , d >. ) /\
               ( C Btwn <. d , R >. /\ <. C , R >. Cgr <. C , E >. ) /\
               ( R Btwn <. P , Q >. /\ <. R , Q >. Cgr <. R , P >. ) ) ) ) ) ->
       D = d ) $=
    ( wcel w3a wa cop cbtwn wbr ccgr wi cee cfv wne wceq simp11 simp2l1 simp2l3
    cn cv simp31 simp32 simp1l3 ad2antrl ccolin ad2antll btwncolinear5 syl13anc
    simpr1l simp2l2 simp2r1 simpr1r simp2rr cgrtrand btwnconn1lem11 cgrcomlrand
    adantr mpd weq simp12 simp13 simp2r2 simp1rr btwnexchand btwnexch3and opeq1
    simp2ll simp3ll breq2d biimpac axbtwnid syl5 exp3a simp1 simp2l simp2r 3jca
    syl3anc simpl simprl jca btwnconn1lem7 syl2an simp2rl simp3rl btwncolinear2
    simp33 simpr2r btwnconn1lem5 breq1d simp2r3 breq12d biimpar necon3d simpr2l
    anbi1d cgrid2 syland mp2and btwncolinear4 simpr3r cgrcomand linecgrand ee21
    opeq2 biimprd adantrl btwncolinear1 simp1rl btwncolinear6 ex btwnconn1lem10
    com12 an12s pm2.61ine biimpa axcgrid eqcomd ) IUHMZAIUAUBZMZBYIMZNZCYIMZDYI
    MZKUIZYIMZNZLUIZYIMZJUIZYIMZHYIMZNZOZEYIMZFYIMZGYIMZNZNZABUCZBCUCZCYOUCZNZB
    ACPQRZBADPQRZOZOZDAYOPQRZDYOPCDPZSRZOZCAYRPQRZCYRPZUUSSRZOZOZYOAYTPZQRZYOYT
    PCBPSRZOZYRUVGQRZYRYTPZDBPSRZOZOZNZHCYOPZQRHDYRPQROZCYOEPZQRZCEPUVCSRZOZCYR
    GPQRZCGPZCHPZSRZOZGEFPZQRZGFPGEPSRZOZNZOZOZOZYRDUWOEFUDZYRDPZUWHSRZYRDUDZUW
    OEEPUWHSRZUWPUUIUWNCYOEEFIYHYJYKUUDUUHUEZYMYNYPUUCYLUUHUFZYMYNYPUUCYLUUHUGZ
    YLUUDUUEUUFUUGUJZUXDYLUUDUUEUUFUUGUKZUVPUULUUIUWMUUJUUKUULUUPUVFUVOULUMZUWO
    UVTCUVSUNRZUWMUVTUUIUVPUVTUWAUWGUWKUVRURUOUUIUVTUXGTZUWNUUIYHYPUUEYMUXHUXAU
    XCUXDUXBYOECIUPUQVFVGUUIUWNCECDCFIUXAUXBUXDUXBYMYNYPUUCYLUUHUSZUXBUXEUUIUWN
    CECYRCDIUXAUXBUXDUXBYSUUAUUBYQYLUUHUTZUXBUXIUWMUWAUUIUVPUVTUWAUWGUWKUVRVAUO
    UVPUVDUUIUWMUVBUVDUVAUUQUVOVBUMVCUUIUWNDCFCIUXAUXIUXBUXEUXBABCDEFGHIJKLVDVE
    VCZUWOUVSYOFPZSRZTBYTUWOBYTUDZUXMUWOUXNKJVHZYTEPZYTFPZSRZUXMUWOYOBYTPZQRZUX
    NUXOTZUUIUWNABYOYTIUXAYHYJYKUUDUUHVIZYHYJYKUUDUUHVJZUXCYSUUAUUBYQYLUUHVKZUU
    IUWNABDYOIUXAUYBUYCUXIUXCUVPUUOUUIUWMUUNUUOUUMUVFUVOVLUMUVPUURUUIUWMUURUUTU
    VEUUQUVOVPUMVMUVPUVHUUIUWMUVHUVIUVNUUQUVFVQUMVNZUUIUXTUYATUWNUUIUXTUXNUXOUX
    TUXNOYOYTYTPZQRZUUIUXOUXNUXTUYGUXNUXSUYFYOQBYTYTVOVRVSUUIYHYPUUAUYGUXOTUXAU
    XCUYDYOYTIVTWGWAWBVFVGUUIUWNCYRYTEFIUXAUXBUXJUYDUXDUXEUUIYLYQUUCNZUVPUVROZC
    YRUCUWNUUIYLYQUUCYLUUDUUHWCYLYQUUCUUHWDYLYQUUCUUHWEWFZUWNUVPUVRUVPUWMWHUVPU
    VRUWLWIWJZABCDHIJKLWKWLZUWOYRCYTPQRZCUVLUNRZUUIUWNACYRYTIUXAUYBUXBUXJUYDUVP
    UVBUUIUWMUVBUVDUVAUUQUVOWMUMZUVPUVKUUIUWMUVKUVMUVJUUQUVFWNUMVNUUIUYMUYNTZUW
    NUUIYHYMUUAYSUYPUXAUXBUYDUXJCYTYRIWOUQVFVGUXKUUIUWNGCYREFIUXAYLUUDUUEUUFUUG
    WPZUXBUXJUXDUXEUWOUULGCUCUXFUWOGCCYOUWOUWFHCPZHYOPZSRZGCUDZCYOUDZTZUWMUWFUU
    IUVPUWCUWFUWBUWKUVRWQUOUUIUYHUYIUYTUWNUYJUYKABCDHIJKLWRWLUUIUWFUYTOZVUCTUWN
    UUIVUDVUAVUBVUDVUAOCCPZUWESRZUYTOZUUIVUBVUAVUDVUGVUAUWFVUFUYTVUAUWDVUEUWESG
    CCXNWSXEVSUUIVUFCHUDZUYTVUBUUIYHYMYMUUBVUFVUHTUXAUXBUXBYSUUAUUBYQYLUUHWTCCH
    IXFUQVUHUYTOVUEUVQSRZUUIVUBVUHVUIUYTVUHVUEUYRUVQUYSSCHCVOCHYOVOXAXBUUIYHYMY
    MYPVUIVUBTUXAUXBUXBUXCCCYOIXFUQWAXGWAWBVFXHXCVGUWOUWCGUVCUNRZUWMUWCUUIUVPUW
    CUWFUWBUWKUVRXDUOUUIUWCVUJTZUWNUUIYHYSUUGYMVUKUXAUXJUYQUXBYRGCIXIUQVFVGUUIU
    WNGFGEIUXAUYQUXEUYQUXDUWMUWJUUIUVPUWIUWJUWBUWGUVRXJUOXKUXKXLZXLZUXOUXMUXRUX
    OUVSUXPUXLUXQSYOYTEVOYOYTFVOXAXOXMYBBYTUCZUWOUXMUUIVUNUWNUXMUUIVUNUWNOZBYTY
    OEFIUXAUYCUYDUXCUXDUXEUUIVUNUWNWIUUIVUOOUXTBYTYOPUNRZUUIUWNUXTVUNUYEXPUUIUX
    TVUPTZVUOUUIYHYKUUAYPVUQUXAUYCUYDUXCBYTYOIXQUQVFVGUUIUWNBEPBFPSRVUNUUIUWNCY
    RBEFIUXAUXBUXJUYCUXDUXEUYLUWOCBYRPQRZCYRBPUNRZUUIUWNABCYRIUXAUYBUYCUXBUXJUV
    PUUNUUIUWMUUNUUOUUMUVFUVOXRUMUYOVNUUIVURVUSTZUWNUUIYHYKYSYMVUTUXAUYCUXJUXBB
    YRCIXSUQVFVGUXKVULXLXPUUIUWNUXRVUNVUMXPXLYCXTYDXLUUIUWTUWPTZUWNUUIYHUUEUUEU
    UFVVAUXAUXDUXDUXEEEFIXFUQVFVGABCDEFGHIJKLYAUUIUWPUWROZUWSTUWNVVBUWQFFPZSRZU
    UIUWSUWPUWRVVDUWPUWHVVCUWQSEFFVOVRYEUUIYHYSYNUUFVVDUWSTUXAUXJUXIUXEYRDFIYFU
    QWAVFXHYG $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  ${
    $d A e p q r $.  $d B e p q r $.  $d C e p q r $.  $d D e p q r $.
    $d N e p q r $.  $d b e p q r $.  $d c e p q r $.  $d d e p q r $.
    $( Lemma for ~ btwnconn1 .  Begin back-filling and eliminating
       hypotheses. $)
    btwnconn1lem13 $p |- (
     ( ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
         ( ( C e. ( EE ` N ) /\ D e. ( EE ` N ) /\ c e. ( EE ` N ) ) /\
           ( d e. ( EE ` N ) /\ b e. ( EE ` N ) ) ) ) /\
         ( ( ( A =/= B /\ B =/= C ) /\
               ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) /\
             ( ( D Btwn <. A , c >. /\ <. D , c >. Cgr <. C , D >. ) /\
               ( C Btwn <. A , d >. /\ <. C , d >. Cgr <. C , D >. ) ) /\
             ( ( c Btwn <. A , b >. /\ <. c , b >. Cgr <. C , B >. ) /\
               ( d Btwn <. A , b >. /\ <. d , b >. Cgr <. D , B >. ) ) ) ) ->
       ( C = c \/ D = d ) ) $=
      ( vp vr wcel w3a cv wa cop cbtwn wbr ccgr adantr ad3antrrr ve cee cfv wne
      vq cn wceq wn df-ne wrex simp2rl simp2ll wb simpl1 simprl1 simpl2 simprrl
      btwncom syl13anc simprl2 simprl3 anbi12d syl5ib wi axpasch syl132anc syld
      jca imp simpll1 axsegcon syl122anc reeanv sylanbrc ad2antrr simprl simprr
      simpr simpl simplrl simprrr simpllr simplrr simp1ll simp1lr simpl1r jca32
      3jca simpll2 simpl3l simpl3r btwnconn1lem12 an4s exp32 rexlimdv rexlimdvv
      syl2an mpd expr syl5bir orrd ) EUFKZAEUBUCZKZBXCKZLZCXCKZDXCKZGMZXCKZLZHM
      ZXCKZFMZXCKZNZNZNZABUDZBCUDZNZBACOPQBADOPQNZNZDAXIOPQZDXIOCDOZRQZNZCAXLOP
      QZCXLOZYERQZNZNZXIAXNOZPQXIXNOCBORQNZXLYMPQXLXNODBORQNZNZLZNZCXIUGZDXLUGZ
      YSUHCXIUDZYRYTCXIUIXRYQUUAYTXRYQUUANZNZUAMZCXIOPQUUDDXLOPQNZUAXCUJZYTXRUU
      BUUFXRUUBCXLAOPQZDXIAOPQZNZUUFUUBYHYDNXRUUIUUBYHYDYQYHUUAYHYJYGYCYPUKSYQY
      DUUAYDYFYKYCYPULSVHXRYHUUGYDUUHXRXBXGXDXMYHUUGUMXBXDXEXQUNZXGXHXJXPXFUOZX
      BXDXEXQUPZXFXKXMXOUQZCAXLEURUSXRXBXHXDXJYDUUHUMUUJXGXHXJXPXFUTZUULXGXHXJX
      PXFVAZDAXIEURUSVBVCXRXBXMXJXDXGXHUUIUUFVDUUJUUMUUOUULUUKUUNUAXLXIACDEVEVF
      VGVIUUCUUEYTUAXCUUCUUDXCKZUUEYTXRUUPUUBUUEYTXRUUPNZUUBUUENZNZCXIIMZOPQCUU
      TOYIRQNZCXLJMZOPQCUVBOCUUDORQNZNZJXCUJIXCUJZYTUUQUVEUURUUQUVAIXCUJZUVCJXC
      UJZUVEUUQXBXJXGXGXMUVFXBXDXEXQUUPVJZXRXJUUPUUOSXRXGUUPUUKSZUVIXRXMUUPUUMS
      ZIXICCXLEVKVLUUQXBXMXGXGUUPUVGUVHUVJUVIUVIXRUUPVRJXLCCUUDEVKVLUVAUVCIJXCX
      CVMVNSUUSUVDYTIJXCXCUUSUUTXCKZUVBXCKZNZUVDYTUUQUVMUURUVDYTUUQUVMNZUURUVDN
      ZNZUVBUUTUEMZOPQUVBUVQOUVBUUTORQNZUEXCUJZYTUVNUVSUVOUVNXBUVKUVLUVLUVKUVSX
      RXBUUPUVMUUJVOUUQUVKUVLVPZUUQUVKUVLVQZUWAUVTUEUUTUVBUVBUUTEVKVLSUVPUVRYTU
      EXCUVPUVQXCKZUVRYTUVNUWBUVOUVRYTUVNUWBNZXFXKXMXOUUPLZNZUVKUWBUVLLZLXSXTUU
      ALZYBNZYLYPLZUUEUVAUVCUVRLZNNYTUVOUVRNZUWCXFUWEUWFXRXFUUPUVMUWBXFXQVSTUWC
      XKUWDUUQXKUVMUWBXFXKXPUUPVTVOUWCXMXOUUPXRXMUUPUVMUWBUUMTXRXOUUPUVMUWBXFXK
      XMXOWATXRUUPUVMUWBWBWHVHUWCUVKUWBUVLUUQUVKUVLUWBVTUVNUWBVRUUQUVKUVLUWBWCW
      HWHUWKUWIUUEUWJUWKUWHYLYPUWKUWGYBUWKXSXTUUAUVOXSUVRYQXSUUAUUEUVDXSXTYBYLY
      PWDTSUVOXTUVRYQXTUUAUUEUVDXSXTYBYLYPWETSUVOUUAUVRYQUUAUUEUVDWBSWHUUBYBUUE
      UVDUVRYAYBYLYPUUAWFTVHUURYLUVDUVRYCYLYPUUAUUEWIVOUWKYNYOUUBYNUUEUVDUVRYNY
      OYCYLUUAWJTUUBYOUUEUVDUVRYNYOYCYLUUAWKTVHWHUUBUUEUVDUVRWBUWKUVAUVCUVRUURU
      VAUVCUVRVTUURUVAUVCUVRWCUVOUVRVRWHWGABCDUUTUVQUVBUUDEFGHWLWQWMWNWOWRWMWNW
      PWRWMWNWOWRWSWTXA $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  ${
    $d A b c d x $.  $d B b c d x $.  $d C b c d x $.  $d D b c d x $.
    $d N b c d x $.
    $( Lemma for ~ btwnconn1 .  Final statement of the theorem when
       ` B =/= C ` . $)
    btwnconn1lem14 $p |- ( (
     ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) /\
       ( ( A =/= B /\ B =/= C ) /\
         ( B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ) ) ->
         ( C Btwn <. A , D >. \/ D Btwn <. A , C >. ) ) $=
      ( vc vd vb vx wcel wa w3a cop cbtwn wbr cv ccgr wrex axsegcon 3jca cn cee
      cfv wne simp1 simp2l simp3r simp3 syl121anc simp3l reeanv sylanbrc adantr
      wo wceq simpl1 simpl2l simprl simpl3l simpl2r syl122anc simprr jca sylibr
      simpl3r weq simplrr simpll simplr simpr btwnconn1lem2 syl2an opeq2 breq2d
      breq1d anbi12d anbi2d biimpac jca32 btwnconn1lem13 syl5 expdimp mpd exp32
      ex an4s rexlimdvv orcom simprrl adantl syl5ibrcom simprll orim12d syl5bi
      ) EUAJZAEUBUCZJZBWPJZKZCWPJZDWPJZKZLZABUDBCUDKBACMZNOBADMZNOKKZKZDAFPZMZN
      OZDXHMCDMZQOZKZCAGPZMZNOZCXNMXKQOZKZKZGWPRFWPRZCXENOZDXDNOZUNZXCXTXFXCXMF
      WPRZXRGWPRZXTXCWOWQXAXBYDWOWSXBUEZWOWQWRXBUFZWOWSWTXAUGWOWSXBUHZFADCDESUI
      XCWOWQWTXBYEYFYGWOWSWTXAUJYHGACCDESUIXMXRFGWPWPUKULUMXGXSYCFGWPWPXGXHWPJZ
      XNWPJZKZXSYCXCYKXFXSYCXCYKKZXFXSKZKZCXHUOZDXNUOZUNZYCYNXHAHPZMZNOXHYRMCBM
      QOKZXNAIPZMZNOZXNUUAMZDBMZQOZKZKZIWPRHWPRZYQYNYTHWPRZUUGIWPRZKZUUIYLUULYM
      YLUUJUUKYLWOWQYIWTWRUUJWOWSXBYKUPZWQWRWOXBYKUQZXCYIYJURZWTXAWOWSYKUSZWQWR
      WOXBYKUTZHAXHCBESVAYLWOWQYJXAWRUUKUUMUUNXCYIYJVBWTXAWOWSYKVEZUUQIAXNDBESV
      AVCUMYTUUGHIWPWPUKVDYNUUHYQHIWPWPYNYRWPJZUUAWPJZKZUUHYQYLUVAYMUUHYQYLUVAK
      ZYMUUHKZKIHVFZYQUVBWOWQWRLZWTXAYILZYJUUSUUTLZLXFXSUUHLUVDUVCUVBUVEUVFUVGY
      LUVEUVAYLWOWQWRUUMUUNUUQTUMZYLUVFUVAYLWTXAYIUUPUURUUOTUMZUVBYJUUSUUTXCYIY
      JUVAVGZYLUUSUUTURZYLUUSUUTVBTTUVCXFXSUUHXFXSUUHVHXFXSUUHVIYMUUHVJTABCDEUU
      AHFGVKVLUVBUVCUVDYQUVCUVDKYMYTXNYSNOZXNYRMZUUEQOZKZKZKZUVBYQUVDUVCUVQUVDU
      UHUVPYMUVDUUGUVOYTUVDUUCUVLUUFUVNUVDUUBYSXNNUUAYRAVMVNUVDUUDUVMUUEQUUAYRX
      NVMVOVPVQVQVRUVBUVQYQUVBUVEUVFYJUUSKZKKXFXSUVPLYQUVQUVBUVEUVFUVRUVHUVIUVB
      YJUUSUVJUVKVCVSUVQXFXSUVPXFXSUVPVHXFXSUVPVIYMUVPVJTABCDEHFGVTVLWEWAWBWCWF
      WDWGWCYQYPYOUNYNYCYOYPWHYNYPYAYOYBYNYAYPXPYMXPYLXFXMXPXQWIWJYPXEXOCNDXNAV
      MVNWKYNYBYOXJYMXJYLXFXJXLXRWLWJYOXDXIDNCXHAVMVNWKWMWNWCWFWDWGWC $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  $( Connectitivy law for betweenness.  Theorem 5.1 of [Schwabhauser]
     p. 39-41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
  btwnconn1 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
     ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
     ( ( A =/= B /\ B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ->
       ( C Btwn <. A , D >. \/ D Btwn <. A , C >. ) ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wo wi wceq jca ex breq1 3ad2ant3
    3anbi3d orc syl6bi adantld simpr1 simpl 3simpc adantl btwnconn1lem14 sylan2
    an12s pm2.61ine ) EFGAEHIZGBUOGJCUOGDUOGJKZABLZBACMZNOZBADMZNOZKZCUTNOZDURN
    OZPZUPVBJZVEQBCBCRZVBVEUPVGVBUQUSVCKVEVGVAVCUQUSBCUTNUAUCVCUQVEUSVCVDUDUBUE
    UFBCLZVFVEUPVHVBVEVHVBJZUPUQVHJZUSVAJZJVEVIVJVKVIUQVHVHUQUSVAUGVHVBUHSVBVKV
    HUQUSVAUIUJSABCDEUKULUMTUNT $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  $( Another connectivity law for betweenness.  Theorem 5.2 of [Schwabhauser]
     p. 41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
  btwnconn2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( ( A =/= B /\ B Btwn <. A , C >. /\ B Btwn <. A , D >. ) ->
      ( C Btwn <. B , D >. \/ D Btwn <. B , C >. ) ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wo btwnconn1 wi btwnexch3 ex mpd
    simpr2 anim1i ad2antrr simpr3 simp3r simp3l jca syld3an3 mpand orim12d mpdd
    adantr ) EFGZAEHIZGBUOGJZCUOGZDUOGZJZKZABLZBACMZNOZBADMZNOZKZCVDNOZDVBNOZPZ
    CBDMNOZDBCMNOZPZABCDEQUTVFVIVLRUTVFJZVGVJVHVKVMVGVJVMVGJVCVGJZVJVMVCVGUTVAV
    CVEUBUCUTVNVJRVFVGABCDESUDUATVMVEVHVKUTVAVCVEUEUTVEVHJVKRZVFUNUPUSURUQJVOUT
    URUQUNUPUQURUFUNUPUQURUGUHABDCESUIUMUJUKTUL $.
    $( [14-Oct-2013] $) $( [9-Oct-2013] $)

  ${
    $d N p $.  $d A p $.  $d B p $.  $d C p $.  $d D p $.
    $( Inner connectivity law for betweenness.  Theorem 5.3 of [Schwabhauser]
       p. 41.  (Contributed by Scott Fenton, 9-Oct-2013.) $)
    btwnconn3 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( ( B Btwn <. A , D >. /\ C Btwn <. A , D >. ) ->
      ( B Btwn <. A , C >. \/ C Btwn <. A , B >. ) ) ) $=
      ( vp wcel wa w3a cop cbtwn wbr wi wb btwncom syl13anc adantr btwnexch3and
      wne mpbid cn cee cfv cv wrex simp1 simp3r simp2l btwndiff syl3anc simprlr
      necomd simpl3r simpl2r simpl2l simpr simprrl simprll simpl3l simprrr 3jca
      wo simpl1 ex btwnconn2 syl122anc syld exp3a rexlimdva mpd ) EUAGZAEUBUCZG
      ZBVLGZHZCVLGZDVLGZHZIZADFUDZJKLZAVTSZHZFVLUEZBADJZKLZCWEKLZHZBACJKLCABJKL
      VBZMZVSVKVQVMWDVKVOVRUFVKVOVPVQUGVKVMVNVRUHDAEFUIUJVSWCWJFVLVSVTVLGZHZWCW
      HWIWLWCWHHZVTASZAVTBJKLZAVTCJKLZIZWIWLWMWQWLWMHZWNWOWPWRAVTWLWAWBWHUKULWR
      ABVTJKLZWOWLWMDBAVTEVKVOVRWKVCZVPVQVKVOWKUMZVMVNVKVRWKUNZVMVNVKVRWKUOZVSW
      KUPZWRWFBDAJZKLZWLWCWFWGUQWLWFXFNZWMWLVKVNVMVQXGWTXBXCXABADEOPQTWLWAWBWHU
      RZRWLWSWONZWMWLVKVMVNWKXIWTXCXBXDABVTEOPQTWRACVTJKLZWPWLWMDCAVTEWTXAVPVQV
      KVOWKUSZXCXDWRWGCXEKLZWLWCWFWGUTWLWGXLNZWMWLVKVPVMVQXMWTXKXCXACADEOPQTXHR
      WLXJWPNZWMWLVKVMVPWKXNWTXCXKXDACVTEOPQTVAVDWLVKWKVMVNVPWQWIMWTXDXCXBXKVTA
      BCEVEVFVGVHVIVJ $.
      $( [14-Oct-2013] $) $( [9-Oct-2013] $)
  $}

  $( If two points fall in the same place in the middle of a segment, then they
     are identical.  (Contributed by Scott Fenton, 16-Oct-2013.) $)
  midofsegid $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( D e. ( EE ` N ) /\ E e. ( EE ` N ) ) ) ->
   ( ( D Btwn <. A , B >. /\ E Btwn <. A , B >. /\
       <. A , D >. Cgr <. A , E >. ) -> D = E ) ) $=
    ( cn wcel cee cfv wa w3a cop wbr ccgr mpd simprr simprl3 endofsegidand expr
    cbtwn wceq wo 3simpa adantl simp2l simp3l simp3r simp2r btwnconn3 syl122anc
    wi simp1 adantr cgrcomand eqcomd jaod ex ) EFGZAEHIZGZBUSGZJZCUSGZDUSGZJZKZ
    CABLZTMZDVGTMZACLZADLZNMZKZCDUAZVFVMJZCVKTMZDVJTMZUBZVNVOVHVIJZVRVMVSVFVHVI
    VLUCUDVFVSVRUKZVMVFURUTVCVDVAVTURVBVEULZURUTVAVEUEZURVBVCVDUFZURVBVCVDUGZUR
    UTVAVEUHACDBEUIUJUMOVOVPVNVQVFVMVPVNVFVMVPJZJDCVFWEADCEWAWBWDWCVFVMVPPVFWEA
    CADEWAWBWCWBWDVHVIVLVPVFQUNRUOSVFVMVQVNVFVMVQJACDEWAWBWCWDVFVMVQPVHVIVLVQVF
    QRSUPOUQ $.
    $( [19-Apr-2014] $) $( [16-Oct-2013] $)

  ${
    $d Q a x $.  $d N a x $.  $d A a x $.  $d B a x $.  $d C a x $.
    $( Generalization of ~ axsegcon .  This time, we generate an endpoint for a
       segment on the ray ` Q A ` congruent to ` B C ` and starting at ` Q ` ,
       as opposed to ~ axsegcon , where the segment starts at ` A `
       (Contributed by Scott Fenton, 14-Oct-2013.)  (Removed unneeded
       inequality, 15-Oct-2013.) $)
    segcon2 $p |- ( ( N e. NN /\
       ( Q e. ( EE ` N ) /\ A e. ( EE ` N ) ) /\
       ( B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
         E. x e. ( EE ` N ) ( ( A Btwn <. Q , x >. \/ x Btwn <. Q , A >. ) /\
           <. Q , x >. Cgr <. B , C >. ) ) $=
      ( va wcel wa w3a cv cop cbtwn wbr wrex wne axsegcon adantr wi mpd cee cfv
      cn ccgr wceq breq1 orbi1d anbi1d rexbidv simp1 simp2 ancomd syl3anc simpr
      wo simpl1 simpl2l simpl3 syl121anc anass df-3an simpr1 wb simpr2r simpl2r
      simprl cgrdegen syl122anc necon3bid mpbird necomd simpr2l btwncomand 3jca
      simpr3 simprr btwnconn2 sylan2br expr anim1d sylanb an32s reximdva simp2l
      rexlimdva simp3 orc anim1i reximi syl pm2.61ne ) FUCHZEFUAUBZHZBWMHZIZCWM
      HDWMHIZJZBEAKZLZMNZWSEBLMNZUOZWTCDLUDNZIZAWMOZEWTMNZXBUOZXDIZAWMOZBEBEUEZ
      XEXIAWMXKXCXHXDXKXAXGXBBEWTMUFUGUHUIWRBEPZIZEBGKZLMNZEXNLBELUDNZIZGWMOZXF
      WRXRXLWRWLWOWNIZXSXRWLWPWQUJZWRWNWOWLWPWQUKULZYAGBEBEFQUMRXMXQXFGWMWRXNWM
      HZXLXQXFSWRYBIZXLXQXFYCXLXQIZIZEXNWSLMNZXDIZAWMOZXFYCYHYDYCWLYBWNWQYHWLWP
      WQYBUPWRYBUNWNWOWLWQYBUQWLWPWQYBURAXNECDFQUSRYEYGXEAWMYCWSWMHZYDYGXESZYCY
      IIWRYBYIIZIZYDYJWRYBYIUTYLYDIYFXCXDYLYDYFXCYDYFIYLXLXQYFJZXCXLXQYFVAYLYMI
      ZXNEPZEXNBLMNZYFJZXCYNYOYPYFYNEXNYNEXNPXLYLXLXQYFVBYNEXNBEYNXPEXNUEXKVCZX
      OXPXLYFYLVDYLXPYRSZYMYLWLWNYBWOWNYSWLWPWQYKUPZWNWOWLWQYKUQZWRYBYIVFZWNWOW
      LWQYKVEZUUAEXNBEFVGVHRTVIVJVKYLYMEBXNFYTUUAUUCUUBXOXPXLYFYLVLVMYLXLXQYFVO
      VNYLYQXCSZYMYLWLYBWNWOYIUUDYTUUBUUAUUCWRYBYIVPXNEBWSFVQVHRTVRVSVTWAWBWCTV
      SWBWETWRXGXDIZAWMOZXJWRWLWNWNWQUUFXTWLWNWOWQWDZUUGWLWPWQWFAEECDFQUSUUEXIA
      WMXGXHXDXGXBWGWHWIWJWK $.
      $( [15-Oct-2013] $)
  $}


