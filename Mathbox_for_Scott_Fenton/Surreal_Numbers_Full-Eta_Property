$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Full-Eta Property
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Lemma for axfe (future) .  The successor of the union of the image of the
     birthday function under a set is an ordinal. $)
  axfelem1 $p |- ( A e. V -> suc U. ( bday " A ) e. On ) $=
    ( wcel cbday cima cuni word cvv wa csuc con0 wfun bdayfun funimaexg mpan
    uniexg syl wss crn imassrn bdayrn sseqtri ssorduni ax-mp jctil elon2
    sucelon bitr3i sylib ) ABCZDAEZFZGZULHCZIZULJKCZUJUNUMUJUKHCZUNDLUJUQMDABNO
    UKHPQUKKRUMUKDSKDATUAUBUKUCUDUEUOULKCUPULUFULUGUHUI $.
    $( [10-Sep-2011] $) $( [20-Aug-2011] $)

  ${
    $d F a b n $.  $d X a b $.
    axfelem2.1 $e |- X e. { 1o , 2o } $.
    axfelem2.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Show a particular abstraction is an
       ordinal. $)
    axfelem2 $p |- ( ( F C_ No /\ F e. A ) -> C e. On ) $=
      ( csur wss wcel wa cv cfv wne wrex wral con0 crab cint cbday cima cuni
      csuc axfelem1 cdm ssel2 bdaydm syl6eleqr simpr wfun wi bdayfun funfvima
      mpan sylc elssuni syl word wb bdayelon crn imassrn bdayrn sseqtri
      ssorduni ax-mp ordsssuc mp2an sylib c0 nosgnn0i wceq axdenselem2 neeq1d
      mpbiri fveq2 rcla4ev syl11anc r19.21aiva rexeq ralbidv syl2an ancoms
      onintrab2 syl5eqel ) DJKZDALZMZBGNZCNZOZEPZGFNZQZCDRZFSTUAZSIWJWQFSQZWRSL
      WIWHWSWIUBDUCZUDZUEZSLWNGXBQZCDRZWSWHDAUFWHXCCDWHWLDLZMZWLUBOZXBLZXGWLOZE
      PZXCXFXGXAKZXHXFXGWTLZXKXFWLUBUGZLZXEXLXFWLJXMDJWLUHZUIUJWHXEUKUBULXNXEXL
      UMUNDWLUBUOUPUQXGWTURUSXGSLXAUTZXKXHVAWLVBWTSKXPWTUBVCSUBDVDVEVFWTVGVHXGX
      AVIVJVKXFXJVLEPEHVMXFXIVLEXFWLJLXIVLVNXOWLVOUSVPVQWNXJGXGXBWKXGVNWMXIEWKX
      GWLVRVPVSVTWAWQXDFXBSWOXBVNWPXCCDWNGWOXBWBWCVSWDWEWQFWFVKWG $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

    $( Lemma for axfe (future) .  Calculate the birthday of
       ` ( C X. { X } ) ` . $)
    axfelem3 $p |- ( ( F C_ No /\ F e. A ) ->
                       ( bday ` ( C X. { X } ) ) = C ) $=
      ( csur wss wcel wa csn cxp cbday cfv cdm con0 wceq axfelem2 noxpsgn
      bdayval 3syl c0 wne c1o c2o cpr elisseti snnz dmxp ax-mp syl6eq ) DJKDALM
      ZBENZOZPQZUQRZBUOBSLUQJLURUSTABCDEFGHIUABEHUBUQUCUDUPUEUFUSBTEEUGUHUIHUJU
      KBUPULUMUN $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

  $}

  ${
    $d A x y $.  $d X x y $.
    axfelem4.1 $e |- X e. { 1o , 2o } $.
    $( Lemma for axfe (future) .  The infimum of the class of all ordinals such
       that ` A ` is not ` X ` is an ordinal. $)
    axfelem4 $p |- ( A e. No ->
                        |^| { x e. On | ( A ` x ) =/= X } e. On ) $=
      ( csur wcel cv cfv wne con0 wrex crab cint cbday bdayelon c0 nosgnn0i
      axdenselem2 neeq1d mpbiri wceq fveq2 rcla4ev sylancr onintrab2 sylib ) BE
      FZAGZBHZCIZAJKZUJAJLMJFUGBNHZJFULBHZCIZUKBOUGUNPCICDQUGUMPCBRSTUJUNAULJUH
      ULUAUIUMCUHULBUBSUCUDUJAUEUF $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

    $( Lemma for axfe (future) .  There is always a minimal value of a surreal
       that is not a given sign. $)
    axfelem5 $p |- ( A e. No ->
                      ( A ` |^| { x e. On | ( A ` x ) =/= X } ) =/= X ) $=
      ( vy csur wcel cv cfv wne con0 crab cint wa wss c0 ssrab2 cbday nosgnn0i
      axdenselem2 neeq1d mpbiri wb bdayelon wceq fveq2 elrab3 ax-mp sylibr
      ne0i syl onint sylancr hbrab1 hbint ax-17 hbfv hbne elrabf sylib simprd
      ) BFGZAHZBIZCJZAKLZMZKGZVGBIZCJZVBVGVFGZVHVJNVBVFKOVFPJZVKVEAKQVBBRIZVFGZ
      VLVBVMBIZCJZVNVBVPPCJCDSVBVOPCBTUAUBVMKGVNVPUCBUDVEVPAVMKVCVMUEVDVOCVCVMB
      UFUAUGUHUIVFVMUJUKVFULUMVEVJAEVGKAEVFVEAEKUNUOZEHZKGAUPAEVICAEVGBVRBGAUPV
      QUQVRCGAUPURVCVGUEVDVICVCVGBUFUAUSUTVA $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A a b x $.  $d F a b $.  $d X a b x $.  $d X n $.  $d a b n $.
    $d A n $.  $d F n $.
    axfelem6.1 $e |- X e. { 1o , 2o } $.
    axfelem6.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Given an element ` A ` of ` F ` , then the
       first position where it differs from ` X ` is strictly less than
       ` C ` $)
    axfelem6 $p |- ( ( F C_ No /\ A e. F ) ->
                     |^| { x e. On | ( A ` x ) =/= X } e. C ) $=
      ( csur wss wcel wa cv cfv wne con0 crab cint wrex wral wi cbday bdayelon
      ssel2 c0 nosgnn0i axdenselem2 neeq1d mpbiri syl wceq fveq2 rcla4ev
      sylancr onintrab2 sylib fveq1 rexbidv rcla4v ad2antlr wel ad2antrr
      simplr onelon anim1i anasss weq elrab intss1 sylbir adantll simprl ontr2
      imp syl22anc exp32 r19.23adv syld r19.21aiva elintrabg biimpar syl11anc
      syl6eleqr ) EKLZBEMZNZAOZBPZFQZARSZTZHOZDOZPZFQZHGOZUAZDEUBZGRSTZCWHWMRMZ
      WTWMWRMZUCZGRUBZWMXAMZWHWKARUAZXBWHBUDPZRMXHBPZFQZXGBUEWHBKMZXJEKBUFXKXJU
      GFQFIUHXKXIUGFBUIUJUKULWKXJAXHRWIXHUMWJXIFWIXHBUNUJUOUPWKAUQURZWHXDGRWHWR
      RMZNZWTWNBPZFQZHWRUAZXCWGWTXQUCWFXMWSXQDBEWOBUMZWQXPHWRXRWPXOFWNWOBUSUJUT
      VAVBXNXPXCHWRXNHGVCZXPXCXNXSXPNZNXBXMWMWNLZXSXCWHXBXMXTXLVDWHXMXTVEXMXTYA
      WHXMXTNWNRMZXPNZYAXMXSXPYCXMXSNYBXPWRWNVFVGVHYCWNWLMYAWKXPAWNRAHVIWJXOFWI
      WNBUNUJVJWNWLVKVLULVMXNXSXPVNXBXMNYAXSNXCWMWNWRVOVPVQVRVSVTWAXBXFXEWTGWMR
      RWBWCWDJWE $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A a b x $.  $d F a b $.  $d X a b x $.  $d a b n $.  $d A n $.
    $d F n $.  $d X n $.
    axfelem7.1 $e |- X e. { 1o , 2o } $.
    axfelem7.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Calculate the value of ` ( C X. { X } ) ` at
       the minimal ordinal whose value is different from ` X ` . $)
    axfelem7 $p |- ( ( F C_ No /\ A e. F ) ->
                      ( ( C X. { X } ) ` |^| { x e. On | ( A ` x ) =/= X } ) =
                      X ) $=
      ( csur wss wcel wa cv cfv wne con0 crab cint csn cxp wceq axfelem6 c1o
      c2o cpr elisseti fvconst2 syl ) EKLBEMNAOBPFQARSTZCMUKCFUAUBPFUCABCDEFGHI
      JUDCFUKFUEUFUGIUHUIUJ $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d F a b n $.  $d S a b $.  $d X a b n $.
    axfelem8.1 $e |- S e. { 1o , 2o } $.
    axfelem8.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= S } $.
    $( Lemma for axfe (future) .  Bound the birthday of ` ( C X. { X } ) `
       above. $)
    axfelem8 $p |- ( ( ( F C_ No /\ F e. A ) /\
                          ( X e. On /\ A. n e. F ( bday ` n ) e. X ) ) ->
                        ( bday ` ( C X. { S } ) ) C_ X ) $=
      ( csur wss wcel wa con0 cv cbday cfv wral csn cxp wne wrex crab cint wi
      ssel2 c0 nosgnn0i axdenselem2 neeq1d mpbiri syl wceq fveq2 rcla4ev
      expcom ralimdva anim2d rexeq ralbidv elrab intss1 sylbir syl6 imp
      syl5eqss adantlr wb axfelem3 sseq1d adantr mpbird ) EKLZEAMZNZFOMZDPZQRZF
      MZDESZNZNBCTUAQRZFLZBFLZVNWBWEVOVNWBNBHPZVRRZCUBZHGPZUCZDESZGOUDZUEZFJVNW
      BWMFLZVNWBVQWHHFUCZDESZNZWNVNWAWPVQVNVTWODEVNVREMNZVSVRRZCUBZVTWOUFWRVRKM
      ZWTEKVRUGXAWTUHCUBCIUIXAWSUHCVRUJUKULUMVTWTWOWHWTHVSFWFVSUNWGWSCWFVSVRUOU
      KUPUQUMURUSWQFWLMWNWKWPGFOWIFUNWJWODEWHHWIFUTVAVBFWLVCVDVEVFVGVHVPWDWEVIW
      BVPWCBFABDECGHIJVJVKVLVM $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)
  $}

  ${
    $d L a b c d k n $.  $d A d x $.  $d a n x z $.  $d b x $.  $d b z $.
    $d c x $.  $d k x $.  $d L x $.  $d L z $.  $d R n $.  $d R y $.  $d R z $.
    $d X a $.  $d X b $.  $d X n $.  $d X z $.

    $( Lemma for axfe (future) .  The full statement of the axiom when ` R ` is
       empty. $)
    axfelem9 $p |- ( R = (/) ->
                      ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                          ( X e. On /\ A. n e. ( L u. R ) ( bday ` n ) e. X )
        /\
                          A. x e. L A. y e. R x <s y ) ->
                        E. z e. No ( ( bday ` z ) C_ X /\
                                     A. x e. L x <s z /\
                                     A. y e. R z <s y ) ) ) $=
      ( vb va vd vc vk c0 wceq csur wss wa wcel con0 cv cbday cfv cun wral
      cslt wbr w3a wrex wi c2o wne crab cint csn cxp c1o 2on elisseti prid2
      eqid axfelem2 noxp2o syl adantr axfelem8 cop ctp ssel2 adantlr axfelem4
      wn wb onss sseld imp ontri1 syl11anc biimpd con2d ex pm2.43d intss1 nsyl
      df-ne weq fveq2 neeq1d elrab3 biimprd syl5bir mt3d axfelem6 onelss sylc
      fvconst2 eqtr4d r19.21aiva w3o wo axfelem5 sylib nofv 3orel3 orcomd
      jctir andir olcd 3orass sylibr fvex 0ex brtp axfelem7 breqtrrd raleq
      breq12d anbi12d rcla4ev syl12anc sltval biimpar syl21anc sseq1d breq2
      ralbidv ad2ant2r 3adant3 uneq2 un0 syl6eq raleqdv anbi2d 3anbi2d 3anbi3d
      df-3an ral0 mpbiran2 syl6bb rexbidv imbi12d mpbiri ) FOPZHQRZFQRZSHDTZFET
      ZSSZIUATZGUBZUCUDITZGHFUEZUFZSZAUBZBUBZUGUHBFUFAHUFZUIZCUBZUCUDZIRZUUPUUT
      UGUHZAHUFZUUTUUQUGUHZBFUFZUIZCQUJZUKUUIUUJUULGHUFZSZUURUIZUVBUVDSZCQUJZUK
      UUIUVJUVMUURUUIUVJUVMUUEUUGUVJUVMUKUUFUUHUUEUUGSZUVJUVMUVNUVJSJUBUUKUDULU
      MJKUBUJGHUFKUAUNUOZULUPUQZQTZUVPUCUDZIRZUUPUVPUGUHZAHUFZUVMUVNUVQUVJUVNUV
      OUATZUVQDUVOGHULKJURULULUAUSUTZVAZUVOVBZVCZUVOVDVEZVFDUVOULGHIKJUWDUWEVGU
      VNUWAUVJUVNUVTAHUVNUUPHTZSZUUPQTZUVQLUBZUUPUDZUWKUVPUDZPZLMUBZUFZUWOUUPUD
      ZUWOUVPUDZUROVHURULVHOULVHVIZUHZSZMUAUJZUVTUUEUWHUWJUUGHQUUPVJVKZUVNUVQUW
      HUWGVFUWINUBZUUPUDZULUMZNUAUNZUOZUATZUWNLUXHUFZUXHUUPUDZUXHUVPUDZUWSUHZUX
      BUWIUWJUXIUXCNUUPULUWDVLVEZUWIUWNLUXHUWIUWKUXHTZSZUWLULUWMUXPUWLULPZUWKUX
      GTZUXPUXHUWKRZUXRUWIUXOUXSVMZUWIUXOUXTUWIUXOUXOUXTUKUXPUXSUXOUXPUXSUXOVMZ
      UXPUXIUWKUATZUXSUYAVNUWIUXIUXOUXNVFUWIUXOUYBUWIUXHUAUWKUWIUXIUXHUARUXNUXH
      VOVEVPVQZUXHUWKVRVSVTWAWBWCVQUWKUXGWDWEUXQVMUWLULUMZUXPUXRUWLULWFUXPUYBUY
      DUXRUKUYCUYBUXRUYDUXFUYDNUWKUANLWGUXEUWLULUXDUWKUUPWHWIWJWKVEWLWMUXPUWKUV
      OTZUWMULPUWIUXOUYEUWIUXHUVOUWKUWIUWBUXHUVOTZUXHUVORUVNUWBUWHUWFVFUUEUWHUY
      FUUGNUUPUVOGHULKJUWDUWEWNVKUVOUXHWOWPVPVQUVOULUWKUWCWQVEWRWSUWIUXKULUXLUW
      SUWIUXKURPZULOPSZUYGULULPZSZUXKOPZUYISZWTZUXKULUWSUHUWIUYHUYJUYLXAZXAUYMU
      WIUYNUYHUWIUYGUYKXAZUYISUYNUWIUYOUYIUWIUYKUYGUWIUXKULPZVMZUYKUYGUYPWTZUYK
      UYGXAUWIUXKULUMZUYQUWIUWJUYSUXCNUUPULUWDXBVEUXKULWFXCUWIUWJUYRUXCUUPUXHXD
      VEUYKUYGUYPXEWPXFULVBXGUYGUYKUYIXHXCXIUYHUYJUYLXJXKUROURULOULUXKULUXHUUPX
      LUWCXMUWCUWCXNXKUUEUWHUXLULPUUGNUUPUVOGHULKJUWDUWEXOVKXPUXAUXJUXMSMUXHUAU
      WOUXHPZUWPUXJUWTUXMUWNLUWOUXHXQUYTUWQUXKUWRUXLUWSUWOUXHUUPWHUWOUXHUVPWHXR
      XSXTYAUWJUVQSUVTUXBMLUUPUVPYBYCYDWSVFUVLUVSUWASCUVPQUUTUVPPZUVBUVSUVDUWAV
      UAUVAUVRIUUTUVPUCWHYEVUAUVCUVTAHUUTUVPUUPUGYFYGXSXTYAWBYHVQYIUUDUUSUVKUVH
      UVMUUDUUOUVJUUIUURUUDUUNUVIUUJUUDUULGUUMHUUDUUMHOUEHFOHYJHYKYLYMYNYOUUDUV
      GUVLCQUUDUVGUVBUVDUVEBOUFZUIZUVLUUDUVFVUBUVBUVDUVEBFOXQYPVUCUVLVUBUVBUVDV
      UBYQUVEBYRYSYTUUAUUBUUC $.
      $( [4-Apr-2012] $) $( [3-Aug-2011] $)
  $}

  ${
    $d a b c d k n y z $.  $d B d y $.  $d L n z $.  $d L x $.
    $d R a b c d n y z $.  $d X a b n z $.
    $( Lemma for axfe (future) .  The full statement of the axiom when ` L ` is
       empty. $)
    axfelem10 $p |- ( L = (/) ->
                      ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                          ( X e. On /\ A. n e. ( L u. R ) ( bday ` n ) e. X )
        /\
                          A. x e. L A. y e. R x <s y ) ->
                        E. z e. No ( ( bday ` z ) C_ X /\
                                     A. x e. L x <s z /\
                                     A. y e. R z <s y ) ) ) $=
      ( vb va vd vc vk c0 wceq csur wss wa wcel con0 cv cbday cfv cun wral
      cslt wbr w3a wrex wi c1o wne crab cint csn cxp c2o 1on elisseti prid1
      eqid axfelem2 noxp1o syl adantr axfelem8 cop ctp ssel2 adantlr axfelem4
      axfelem6 onelss sylc sseld imp fvconst2 wn wb onss ontri1 syl11anc
      biimpd con2d ex pm2.43d intss1 nsyl df-ne weq fveq2 neeq1d elrab3
      biimprd syl5bir mt3d eqtr4d r19.21aiva axfelem7 w3o wo axfelem5 sylib
      nofv 3orel2 jctil andi orcd df-3or sylibr fvex 0ex 2on brtp eqbrtrd
      raleq breq12d anbi12d rcla4ev syl12anc sltval biimpar syl21anc sseq1d
      breq1 ralbidv ad2ant2l 3adant3 uneq1 uncom un0 eqtri syl6eq raleqdv
      anbi2d 3anbi2d 3anass ral0 biantrur anbi2i bitr4i syl6bb rexbidv imbi12d
      mpbiri ) HOPZHQRZFQRZSHDTZFETZSSZIUATZGUBZUCUDITZGHFUEZUFZSZAUBZBUBZUGUHB
      FUFAHUFZUIZCUBZUCUDZIRZUUSUVCUGUHZAHUFZUVCUUTUGUHZBFUFZUIZCQUJZUKUULUUMUU
      OGFUFZSZUVAUIZUVEUVISZCQUJZUKUULUVMUVPUVAUULUVMUVPUUIUUKUVMUVPUKUUHUUJUUI
      UUKSZUVMUVPUVQUVMSJUBUUNUDULUMJKUBUJGFUFKUAUNUOZULUPUQZQTZUVSUCUDZIRZUVSU
      UTUGUHZBFUFZUVPUVQUVTUVMUVQUVRUATZUVTEUVRGFULKJULURULUAUSUTZVAZUVRVBZVCZU
      VRVDVEZVFEUVRULGFIKJUWGUWHVGUVQUWDUVMUVQUWCBFUVQUUTFTZSZUVTUUTQTZLUBZUVSU
      DZUWNUUTUDZPZLMUBZUFZUWRUVSUDZUWRUUTUDZULOVHULURVHOURVHVIZUHZSZMUAUJZUWCU
      VQUVTUWKUWJVFUUIUWKUWMUUKFQUUTVJVKZUWLNUBZUUTUDZULUMZNUAUNZUOZUATZUWQLUXK
      UFZUXKUVSUDZUXKUUTUDZUXBUHZUXEUWLUWMUXLUXFNUUTULUWGVLVEZUWLUWQLUXKUWLUWNU
      XKTZSZUWOULUWPUXSUWNUVRTZUWOULPUWLUXRUXTUWLUXKUVRUWNUWLUWEUXKUVRTZUXKUVRR
      UVQUWEUWKUWIVFUUIUWKUYAUUKNUUTUVRGFULKJUWGUWHVMVKUVRUXKVNVOVPVQUVRULUWNUW
      FVRVEUXSUWPULPZUWNUXJTZUXSUXKUWNRZUYCUWLUXRUYDVSZUWLUXRUYEUWLUXRUXRUYEUKU
      XSUYDUXRUXSUYDUXRVSZUXSUXLUWNUATZUYDUYFVTUWLUXLUXRUXQVFUWLUXRUYGUWLUXKUAU
      WNUWLUXLUXKUARUXQUXKWAVEVPVQZUXKUWNWBWCWDWEWFWGVQUWNUXJWHWIUYBVSUWPULUMZU
      XSUYCUWPULWJUXSUYGUYIUYCUKUYHUYGUYCUYIUXIUYINUWNUANLWKUXHUWPULUXGUWNUUTWL
      WMWNWOVEWPWQWRWSUWLUXNULUXOUXBUUIUWKUXNULPUUKNUUTUVRGFULKJUWGUWHWTVKUWLUL
      ULPZUXOOPZSZUYJUXOURPZSZULOPUYMSZXAZULUXOUXBUHUWLUYLUYNXBZUYOXBUYPUWLUYQU
      YOUWLUYJUYKUYMXBZSUYQUWLUYRUYJUWLUXOULPZVSZUYKUYSUYMXAZUYRUWLUXOULUMZUYTU
      WLUWMVUBUXFNUUTULUWGXCVEUXOULWJXDUWLUWMVUAUXFUUTUXKXEVEUYKUYSUYMXFVOULVBX
      GUYJUYKUYMXHXDXIUYLUYNUYOXJXKULOULUROURULUXOUWFUXKUUTXLXMURUAXNUTZVUCXOXK
      XPUXDUXMUXPSMUXKUAUWRUXKPZUWSUXMUXCUXPUWQLUWRUXKXQVUDUWTUXNUXAUXOUXBUWRUX
      KUVSWLUWRUXKUUTWLXRXSXTYAUVTUWMSUWCUXEMLUVSUUTYBYCYDWSVFUVOUWBUWDSCUVSQUV
      CUVSPZUVEUWBUVIUWDVUEUVDUWAIUVCUVSUCWLYEVUEUVHUWCBFUVCUVSUUTUGYFYGXSXTYAW
      FYHVQYIUUGUVBUVNUVKUVPUUGUURUVMUULUVAUUGUUQUVLUUMUUGUUOGUUPFUUGUUPOFUEZFH
      OFYJVUFFOUEFOFYKFYLYMYNYOYPYQUUGUVJUVOCQUUGUVJUVEUVFAOUFZUVIUIZUVOUUGUVGV
      UGUVEUVIUVFAHOXQYQVUHUVEVUGUVISZSUVOUVEVUGUVIYRUVIVUIUVEVUGUVIUVFAYSYTUUA
      UUBUUCUUDUUEUUF $.
      $( [4-Apr-2012] $) $( [3-Aug-2011] $)
  $}

  ${
    $d a b m n $.  $d p a q $.  $d m b n $.  $d p b q $.  $d L a b n p $.
    $d m n q $.  $d n p q $.  $d R a b m n $.  $d R p q $.
    axfelem11.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    $( Lemma for axfe (future) .  We are now going to construct a new surreal
       for the case where ` L ` and ` R ` are not null.  The birthday/domain of
       this surreal will be the intersection of ` D ` .  This lemma just
       changes bound variables for later use. $)
    axfelem11 $p |- D = { b e. On | A. n e. L A. m e. R
                            ( n |` b ) =/= ( m |` b ) } $=
      ( cv cres wne wral con0 crab wceq reseq2 neeq12d 2ralbidv reseq1 neeq1d
      neeq2d cbvral2v syl6bb cbvrabv eqtri ) AGKZHKZLZFKZUILZMZFBNGENZHOPDKZIKZ
      LZCKZUPLZMZCBNDENZIOPJUNVAHIOUIUPQZUNUHUPLZUKUPLZMZFBNGENVAVBUMVEGFEBVBUJ
      VCULVDUIUPUHRUIUPUKRSTVEUTUQVDMGFDCEBUHUOQVCUQVDUHUOUPUAUBUKURQVDUSUQUKUR
      UPUAUCUDUEUFUG $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)
  $}

  ${
    $d a b c d e f $.  $d g h a b c $.  $d h d $.  $d f g h $.  $d L b c f g $.
    $d P a b c d e f $.  $d P g h $.  $d R b c d f $.  $d R g h $.
    axfelem12.1 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                      ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    $( Lemma for axfe (future) .  The surreal mentioned in the previous lemma
       will ultimately be the union of ` C ` .  Again, we just change bound
       variables for later. $)
    axfelem12 $p |- C = { b | E. c e. L E. d e. R E. e e. P
                     ( ( c |` e ) = b /\ ( d |` e ) = b ) } $=
      ( cv cres wceq wa wrex cab eqeq2 anbi12d rexbidv 2rexbidv reseq1 eqeq1d
      anbi1d cbvrexv anbi2d reseq2 rexbii bitri syl6bb cbvabv eqtri ) AFNZINZOZ
      ENZPZGNZUPOZURPZQZIBRZGCRFHRZESKNZDNZOZJNZPZLNZVGOZVIPZQZDBRZLCRZKHRZJSMV
      EVQEJURVIPZVEUQVIPZVAVIPZQZIBRZGCRZFHRZVQVRVDWBFGHCVRVCWAIBVRUSVSVBVTURVI
      UQTURVIVATUAUBUCWDVFUPOZVIPZVTQZIBRZGCRZKHRVQWCWIFKHUOVFPZWAWGGICBWJVSWFV
      TWJUQWEVIUOVFUPUDUEUFUCUGWIVPKHWIWFVKUPOZVIPZQZIBRZLCRVPWHWNGLCUTVKPZWGWM
      IBWOVTWLWFWOVAWKVIUTVKUPUDUEUHUBUGWNVOLCWMVNIDBUPVGPZWFVJWLVMWPWEVHVIUPVG
      VFUIUEWPWKVLVIUPVGVKUIUEUAUGUJUKUJUKULUMUN $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

  $}

  $( Lemma for axfe (future) .  Establish an equality result for restriction of
     surreals. $)
  axfelem13 $p |- ( ( A C_ No /\ X e. A ) ->
                    ( X |` suc U. ( bday " ( A u. B ) ) ) = X ) $=
    ( csur wss wcel wa wrel cdm cbday cun cima cuni csuc cres wceq ssel2 wfun
    nofun funrel syl cfv bdayval bdaydm syl6eleqr bdayfun jctil wi funfvima
    com12 adantl mpd ssun1 imass2 ax-mp sseli elssuni sssucid sstr2 mpi
    eqsstr3d relssres syl11anc ) ADEZCAFZGZCHZCIZJABKZLZMZNZECVLOCPVFCDFZVGADCQ
    ZVMCRVGCSCTUAUAVFVHCJUBZVLVFVMVOVHPVNCUCUAVFVOVKEZVOVLEZVFVOVJFZVPVFVOJALZF
    ZVRVFJRZCJIZFZGZVTVFWCWAVFCDWBVNUDUEUFUGVEWDVTUHVDWDVEVTACJUIUJUKULVSVJVOAV
    IEVSVJEABUMAVIJUNUOUPUAVOVJUQUAVPVKVLEVQVKURVOVKVLUSUTUAVACVLVBVC $.
    $( [12-May-2012] $) $( [22-Apr-2012] $)

  ${
    $d p a q $.  $d L a p $.  $d L x $.  $d R a p q $.  $d R x y $.
    $d p a q b m n $.  $d L b m n $.  $d R b m n $.  $d x y b m n $.
    axfelem14.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem14.2 $e |- P = |^| D $.
    $( Lemma for axfe (future) .  Now, we prove that ` P ` , the birthday of a
       surreal we are going to be working with, is actually an ordinal. $)
    axfelem14 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\
                           ( L e. A /\ R e. B ) ) /\
                          A. x e. L A. y e. R x <s y ) -> P e. On ) $=
      ( vn vb vm csur wss wa wcel cv cslt wbr wral cres wne con0 wrex cbday
      cun cima cuni csuc cvv unexg axfelem1 syl ad2antlr wi breq1 breq2
      rcla42v impcom wn wceq ssel2 sltirr notbid syl5ibcom necon2ad ad2ant2r
      axfelem13 uncom imaeq2i unieqi suceq ax-mp reseq2i syl5eq ad2ant2l
      eqeq12d necon3bid sylibrd ex com23 syl5 expdimp pm2.43d r19.21aivv
      adantlr reseq2 2ralbidv rcla4ev syl11anc crab cint axfelem11 inteqi
      eqtri eleq1i onintrab2 bitr4i sylibr ) HQRZGQRZSZHCTGDTSZSAUAZBUAZUBUCZBG
      UDAHUDZSZNUAZOUAZUEZPUAZXNUEZUFZPGUDNHUDZOUGUHZFUGTZXLUIHGUJZUKZULZUMZUGT
      ZXMYEUEZXPYEUEZUFZPGUDNHUDZXTXGYFXFXKXGYBUNTYFHGCDUOYBUNUPUQURXFXKYJXGXFX
      KSZYINPHGYKXMHTZXPGTZSZYIXFXKYNYNYIUSZXKYNSXMXPUBUCZXFYOYNXKYPXJYPXMXIUBU
      CABXMXPHGXHXMXIUBUTXIXPXMUBVAVBVCXFYNYPYIXFYNYPYIUSXFYNSZYPXMXPUFZYIXDYLY
      PYRUSXEYMXDYLSZYPXMXPYSXMXMUBUCZVDZXMXPVEZYPVDYSXMQTUUAHQXMVFXMVGUQUUBYTY
      PXMXPXMUBVAVHVIVJVKYQYGYHXMXPYQYGXMYHXPXDYLYGXMVEXEYMHGXMVLVKXEYMYHXPVEXD
      YLXEYMSYHXPUIGHUJZUKZULZUMZUEXPYEUUFXPYDUUEVEYEUUFVEYCUUDYBUUCUIHGVMVNVOY
      DUUEVPVQVRGHXPVLVSVTWAWBWCWDWEWFWGWHWIWJXSYJOYEUGXNYEVEZXRYINPHGUUGXOXQYG
      YHUUGXOYGXQYHXNYEXMWKXNYEXPWKWAWBWLWMWNYAXSOUGWOZWPZUGTXTFUUIUGFEWPUUIMEU
      UHEGPNHIJKOLWQWRWSWTXSOXAXBXC $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    $( Lemma for axfe (future) .  For non-empty ` L ` and ` R ` , ` P ` is
       non-empty. $)
    axfelem15 $p |- ( ( L =/= (/) /\ R =/= (/) ) -> P =/= (/) ) $=
      ( vn vb vm c0 wne wa cv cres wral con0 crab wcel wn wex wceq wrex eqidd
      ancli 2eximi r2ex sylibr n0 anbi12i eeanv bitr4i df-ne con2bii rexbii
      rexnal bitri bitr2i 3imtr4i reseq2 res0 syl6eq neeq12d 2ralbidv elrab
      simprbi nsyl cint axfelem11 inteqi eqtri neeq1i wss wb ssrab2 onint0
      ax-mp necon3abii ) DMNZCMNZOZMJPZKPZQZLPZWEQZNZLCRJDRZKSTZUAZUBZBMNZWCMMN
      ZLCRZJDRZWLWDDUAZWGCUAZOZLUCJUCZMMUDZLCUEZJDUEZWCWQUBZXAWTXBOZLUCJUCXDWTX
      FJLWTXBWTMUFUGUHXBJLDCUIUJWCWRJUCZWSLUCZOXAWAXGWBXHJDUKLCUKULWRWSJLUMUNXD
      WPUBZJDUEXEXCXIJDXCWOUBZLCUEXIXBXJLCWOXBMMUOUPUQWOLCURUSUQWPJDURUTVAWLMSU
      AWQWJWQKMSWEMUDZWIWOJLDCXKWFMWHMXKWFWDMQMWEMWDVBWDVCVDXKWHWGMQMWEMWGVBWGV
      CVDVEVFVGVHVIWNWKVJZMNWMBXLMBAVJXLIAWKACLJDEFGKHVKVLVMVNWLXLMWKSVOXLMUDWL
      VPWJKSVQWKVRVSVTUSUJ $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d Q y $.  $d R x y $.
    $( Lemma for axfe (future) .  Preliminary work for ~ axfelem17 . $)
    axfelem16 $p |- ( ( ( ( A C_ No /\ B C_ No ) /\
                           A. x e. A A. y e. B x <s y ) /\
                         ( X e. On /\
                           ( ( P e. A /\ Q e. B ) /\
                             ( R e. A /\ S e. B ) ) ) /\
                         ( ( P |` X ) = ( Q |` X ) /\
                           ( R |` X ) = ( S |` X ) ) ) ->
                       ( -. ( P |` X ) <s ( R |` X ) /\
                         -. ( Q |` X ) <s ( S |` X ) ) ) $=
      ( csur wss wa cv cslt wbr wral con0 wcel cres wceq wn simplll simprrl
      adantl sseldd simpllr simprlr jca adantr breq1 breq2 rcla42v sylc
      sltasym wi biimpd simprl sltres syl111anc sylan9r mtod biimprd 3impa ) CJ
      KZDJKZLZAMZBMZNOZBDPACPZLZIQRZECRZFDRZLZGCRZHDRZLZLZLZEISZFISZTZGISZHISZT
      ZLZWAWDNOZUAZWBWENOZUAZLVKVTLZWGLZWIWKWMWHFGNOZWMGJRZFJRZLZGFNOZWNUAWLWQW
      GWLWOWPWLCJGVDVEVJVTUBVTVPVKVLVOVPVQUCUDZUEZWLDJFVDVEVJVTUFVTVNVKVLVMVNVR
      UGUDZUEZUHUIWMVPVNLZVJWRWLXCWGWLVPVNWSXAUHUIVFVJVTWGUFVIWRGVHNOABGFCDVGGV
      HNUJVHFGNUKULUMGFUNUMZWGWHWBWDNOZWLWNWCWHXEUOWFWCWHXEWAWBWDNUJUPUIWLWPWOV
      LXEWNUOXBWTVKVLVSUQFGIURUSZUTVAWMWJWNXDWGWJXEWLWNWFWJXEUOWCWFXEWJWDWEWBNU
      KVBUDXFUTVAUHVC $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    ${
      $d P x y $.  $d S y $.
      $( Lemma for axfe (future) .  A uniqueness result for restrictions of
         surreals. $)
      axfelem17 $p |- ( ( ( ( A C_ No /\ B C_ No ) /\
                           A. x e. A A. y e. B x <s y ) /\
                         ( X e. On /\
                           ( ( P e. A /\ Q e. B ) /\
                             ( R e. A /\ S e. B ) ) ) /\
                         ( ( P |` X ) = ( Q |` X ) /\
                           ( R |` X ) = ( S |` X ) ) ) ->
                       ( ( P |` X ) = ( R |` X ) /\
                         ( Q |` X ) = ( S |` X ) ) ) $=
        ( csur wss wa cv cslt wbr wral con0 wcel cres wceq w3a wn axfelem16
        simp1 simp2l simp2rr simp2rl jca32 simp3r simp3l syl112anc jca an4
        sylib wb simp1ll simprll 3ad2ant2 sseldd noreson syl11anc simprrl
        simp1lr simprlr simprrr slttrieq2 bi2anan9 syl22anc mpbird ) CJKZDJKZLA
        MBMNOBDPACPZLZIQRZECRZFDRZLZGCRZHDRZLZLZLZEISZFISZTZGISZHISZTZLZUAZWCWF
        TZWDWGTZLZWCWFNOUBZWFWCNOUBZLZWDWGNOUBZWGWDNOUBZLZLZWJWNWQLZWOWRLZLWTWJ
        XAXBABCDEFGHIUCWJVMVNVTVQLLWHWEXBVMWBWIUDWJVNVTVQVMVNWAWIUEZVQVTVNVMWIU
        FVQVTVNVMWIUGUHVMWBWEWHUIVMWBWEWHUJABCDGHEFIUCUKULWNWQWOWRUMUNWJWCJRZWF
        JRZWDJRZWGJRZWMWTUOWJEJRVNXDWJCJEVJVKVLWBWIUPZWBVMVOWIVNVOVPVTUQURUSXCE
        IUTVAWJGJRVNXEWJCJGXHWBVMVRWIVNVQVRVSVBURUSXCGIUTVAWJFJRVNXFWJDJFVJVKVL
        WBWIVCZWBVMVPWIVNVOVPVTVDURUSXCFIUTVAWJHJRVNXGWJDJHXIWBVMVSWIVNVQVRVSVE
        URUSXCHIUTVAXDXELWKWPXFXGLWLWSWCWFVFWDWGVFVGVHVI $.
        $( [12-May-2012] $) $( [22-Apr-2012] $)
    $}
  $}

  ${
    $d f a g h $.  $d a p q $.  $d f g h $.  $d L a f g $.  $d L p $.
    $d L x $.  $d P a f g h $.  $d R a f g h $.  $d R p q $.  $d R x y $.
    $d a b c d e j $.  $d A b c d e j $.  $d B b c d e j $.  $d C b c d e j $.
    $d f g h b c d e j $.  $d L b c d e j $.  $d P b c d e j $.
    $d R b c d e j $.  $d p q b c d e j $.  $d x y b c d e j $.
    axfelem18.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem18.2 $e |- P = |^| D $.
    axfelem18.3 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    axfelem18.4 $e |- M = U. C $.
    $( Lemma for axfe (future) .  At this point, we introduce a new surreal
       number ` M ` (for "middle").  The next several lemmas prove that either
       ` M ` or a closely related surreal has the required properties for the
       final theorem.  We begin by calculating the domain of ` M ` . $)
    axfelem18 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                         A. x e. L A. y e. R x <s y ) -> dom M = U. P ) $=
      ( vb vc ve vd vj csur wss wa wcel cv cslt wbr wral cdm ciun cuni dmeqi
      dmuni eqtri iunss cres wceq wrex axfelem12 abeq2i wi cin elssuni ssinss1
      syl dmres dmeq syl5eqr sseq1d syl5ibcom adantrd r19.23aiv a1i r19.23aivv
      sylbi mprgbir wex csuc word wb con0 axfelem14 eloni ordsucuniel biimpd
      wo wne wn onelon sylan cint crab ssrab2 eqsstri onnmin mpan con2i eleq2s
      adantl reseq2 eqeq12d necon3bid 2ralbidv axfelem11 elrab2 notbii sylib
      imnan sylibr mpd df-ne con2bii rexbii rexnal bitri weq simplll adantr
      simprrl sseldd sltirr breq1 breq2 rcla42v impcom ad2ant2l syl5ibrcom
      mtod ioran adantrr ordsuc nodmord ordtri2or syl11anc orcanai sssucid
      sstr2 mpi jca ex simpllr simprrr anim12d wrel wfun nofun funrel relssres
      anim12i syl6 eqtr2 eqtr3 expcom eqcoms syl5 exp3a com23 imp syl5bi mt3d
      anassrs ancld reximdva cvv visset resexg ax-mp eqeq2 eqcom syl6bb
      anbi12d eqid biantrur syl6bbr eleq2d elin eleq2i sucid 3bitr4ri cla4ev
      biantru jaodan reximi sylbid jcad eqeq1d anbi1d exbidv 2rexbidv rcla4ev
      eliun df-rex anbi1i exbii r19.41v rexcom4 rexcom 3bitr3ri 3bitr3i
      3bitr2i 3bitri syl6ibr ssrdv eqssd syl5eq ) LUFUGZHUFUGZUHLCUIHDUIUHZUHZA
      UJZBUJZUKULZBHUMALUMZUHZMUNZUAEUAUJZUNZUOZGUPZUXTEUPZUNUYCMUYETUQUAEURUSU
      XSUYCUYDUYCUYDUGZUXSUYFUYBUYDUGZUAEUAEUYBUYDUTUYAEUIZUBUJZUCUJZVAZUYAVBZU
      DUJZUYJVAZUYAVBZUHZUCGVCZUDHVCZUBLVCZUYGUYSUAEEGHUCIJKLPUAUBUDSVDVEZUYQUY
      GUBUDLHUYQUYGVFUYILUIZUYMHUIZUHZUYPUYGUCGUYJGUIZUYLUYGUYOVUDUYJUYIUNZVGZU
      YDUGZUYLUYGVUDUYJUYDUGVUGUYJGVHUYJVUEUYDVIVJUYLVUFUYBUYDUYLVUFUYKUNUYBUYI
      UYJVKUYKUYAVLVMVNVOVPVQVRVSVTWAVRUXSUEUYDUYCUXSUEUJZUYDUIZUYPVUHUYBUIZUHZ
      UAWBZUDHVCZUBLVCZUCGVCZVUHUYCUIZUXSVUIVUHWCZGUIZUYIVUQVAZUYAVBZUYMVUQVAZU
      YAVBZUHZVUJUHZUAWBZUDHVCZUBLVCZUHVUOUXSVUIVURVVGUXSVUIVURUXSGWDZVUIVURWEU
      XSGWFUIZVVHABCDFGHLNOPQRWGZGWHVJVUHGWIVJZWJUXSVUIVURVVGVVKUXSVURVVGUXSVUR
      UHZVUSVVAVBZVUHVUEUIZVUHUYMUNZUIZWKZUHZUDHVCZUBLVCZVVGVVLVVMUDHVCZUBLVCZV
      VTVVLVUSVVAWLZUDHUMZUBLUMZWMZVWBVVLVUQWFUIZVWFUXSVVIVURVWGVVJGVUQWNWOZVVL
      VWGVWEUHZWMZVWGVWFVFVVLVUQFUIZWMZVWJVURVWLUXSVWLVUQFWPZGVWKVUQVWMUIZFWFUG
      VWKVWNWMFOUJPUJZVANUJVWOVAWLNHUMOLUMZPWFWQWFQVWPPWFWRWSFVUQWTXAXBRXCXDVWK
      VWIUYIUYAVAZUYMUYAVAZWLZUDHUMUBLUMVWEUAVUQWFFUYAVUQVBZVWSVWCUBUDLHVWTVWQV
      WRVUSVVAVWTVWQVUSVWRVVAUYAVUQUYIXEUYAVUQUYMXEXFXGXHFHUDUBLNOPUAQXIXJXKXLV
      WGVWEXMXNXOVWBVWDWMZUBLVCVWFVWAVXAUBLVWAVWCWMZUDHVCVXAVVMVXBUDHVWCVVMVUSV
      VAXPXQXRVWCUDHXSXTXRVWDUBLXSXTXNVVLVWAVVSUBLVVLVUAUHZVVMVVRUDHVXCVUBUHVVM
      VVQVVLVUAVUBVVMVVQVFZUXSVURVUCVXDUXSVURVUCUHZUHZVVMVVQVXFVVMUHZVVQUBUDYAZ
      VXFVXHWMVVMVXFVXHUYIUYIUKULZVXFUYIUFUIZVXIWMVXFLUFUYIUXSUXKVXEUXKUXLUXMUX
      RYBYCUXSVURVUAVUBYDYEZUYIYFVJVXFVXIVXHUYIUYMUKULZUXRVUCVXLUXNVURVUCUXRVXL
      UXQVXLUYIUXPUKULABUYIUYMLHUXOUYIUXPUKYGUXPUYMUYIUKYHYIYJYKUYIUYMUYIUKYHYL
      YMYCVVQWMVVNWMZVVPWMZUHZVXGVXHVVNVVPYNVXFVVMVXOVXHVFVXFVXOVVMVXHVXFVXOVUS
      UYIVBZVVAUYMVBZUHZVVMVXHVFZVXFVXOVXJVUEVUQUGZUHZUYMUFUIZVVOVUQUGZUHZUHVXR
      VXFVXMVYAVXNVYDVXFVXMVYAVXFVXMUHZVXJVXTVXFVXJVXMVXKYCVYEVUEVUHUGZVXTVXFVV
      NVYFVXFVUHWDZVUEWDZVVNVYFWKVXFVWGVYGUXSVURVWGVUCVWHYOVWGVUQWDVYGVUQWHVUHY
      PXNVJZVXFVXJVYHVXKUYIYQVJVUHVUEYRYSYTVYFVUHVUQUGZVXTVUHUUAZVUEVUHVUQUUBUU
      CVJUUDUUEVXFVXNVYDVXFVXNUHZVYBVYCVXFVYBVXNVXFHUFUYMUXSUXLVXEUXKUXLUXMUXRU
      UFYCUXSVURVUAVUBUUGYEZYCVYLVVOVUHUGZVYCVXFVVPVYNVXFVYGVVOWDZVVPVYNWKVYIVX
      FVYBVYOVYMUYMYQVJVUHVVOYRYSYTVYNVYJVYCVYKVVOVUHVUQUUBUUCVJUUDUUEUUHVYAVXP
      VYDVXQVXJUYIUUIZVXTVXPVXJUYIUUJVYPUYIUUKUYIUULVJUYIVUQUUMWOVYBUYMUUIZVYCV
      XQVYBUYMUUJVYQUYMUUKUYMUULVJUYMVUQUUMWOUUNUUOVXQVXPVXSVXQVXPVVMVXHVXPVVMU
      HUYIVVAVBZVXQVXHVUSUYIVVAUUPVYRVXHVFUYMVVAVYRUYMVVAVBVXHUYIUYMVVAUUQUURUU
      SUUTUVAYJUUOUVBUVCUVDUVEUUEUVFUVFUVGUVHUVHXOVVSVVFUBLVVRVVEUDHVVMVVNVVEVV
      PVVDVVMVVNUHUAVUSUYIUVIUIVUSUVIUIUBUVJUYIVUQUVIUVKUVLUYAVUSVBZVVCVVMVUJVV
      NVYSVVCVUSVUSVBZVVMUHVVMVYSVUTVYTVVBVVMUYAVUSVUSUVMVYSVVBVVAVUSVBVVMUYAVU
      SVVAUVMVVAVUSUVNUVOUVPVYTVVMVUSUVQUVRUVSVYSVUJVUHVUSUNZUIZVVNVYSUYBWUAVUH
      UYAVUSVLUVTVUHVUQVUEVGZUIVUHVUQUIZVVNUHWUBVVNVUHVUQVUEUWAWUAWUCVUHUYIVUQV
      KUWBWUDVVNVUHUEUVJUWCZUVRUWDUVSUVPUWEVVDVVMVVPUHUAVVAUYMUVIUIVVAUVIUIUDUV
      JUYMVUQUVIUVKUVLUYAVVAVBZVVCVVMVUJVVPWUFVVCVVMVVAVVAVBZUHVVMWUFVUTVVMVVBW
      UGUYAVVAVUSUVMUYAVVAVVAUVMUVPWUGVVMVVAUVQUWFUVSWUFVUJVUHVVAUNZUIZVVPWUFUY
      BWUHVUHUYAVVAVLUVTVUHVUQVVOVGZUIWUDVVPUHWUIVVPVUHVUQVVOUWAWUHWUJVUHUYMVUQ
      VKUWBWUDVVPWUEUVRUWDUVSUVPUWEUWGUWHUWHVJUUEUWIUWJVUNVVGUCVUQGUYJVUQVBZVUL
      VVEUBUDLHWUKVUKVVDUAWUKUYPVVCVUJWUKUYLVUTUYOVVBWUKUYKVUSUYAUYJVUQUYIXEUWK
      WUKUYNVVAUYAUYJVUQUYMXEUWKUVPUWLUWMUWNUWOUUOVUPVUJUAEVCUYHVUJUHZUAWBZVUOU
      AVUHEUYBUWPVUJUAEUWQWUMUYSVUJUHZUAWBVUKUCGVCZUDHVCZUBLVCZUAWBZVUOWULWUNUA
      UYHUYSVUJUYTUWRUWSWUQWUNUAWUQUYRVUJUHZUBLVCWUNWUPWUSUBLWUPUYQVUJUHZUDHVCW
      USWUOWUTUDHUYPVUJUCGUWTXRUYQVUJUDHUWTXTXRUYRVUJUBLUWTXTUWSWUPUAWBZUBLVCVU
      MUCGVCZUBLVCWURVUOWVAWVBUBLVULUCGVCZUDHVCWUOUAWBZUDHVCWVBWVAWVCWVDUDHVUKU
      CUAGUXAXRVULUDUCHGUXBWUOUDUAHUXAUXCXRWUPUBUALUXAVUMUBUCLGUXBUXDUXEUXFUXGU
      XHUXIUXJ $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    $( Lemma for axfe (future) . ` C ` is a subset of the surreals. $)
    axfelem19 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> C C_ No ) $=
      ( vc ve vb vd csur wss wa wcel cv cslt wbr wral cres wceq wrex cab
      axfelem12 wi simplll sseld con0 axfelem14 onelon ex syl noreson eleq1
      biimpcd adantrd sylan9 r19.23adv syld r19.23advv abssdv syl5eqss ) LUEUFZ
      HUEUFZUGLCUHHDUHUGZUGAUIBUIUJUKBHULALULZUGZEUAUIZUBUIZUMZUCUIZUNZUDUIZWBU
      MWDUNZUGZUBGUOZUDHUOUALUOZUCUPUEEGHUBIJKLPUCUAUDSUQVTWJUCUEVTWIWDUEUHZUAU
      DLHVTWALUHZWIWKURZWFHUHVTWLWAUEUHZWMVTLUEWAVPVQVRVSUSUTVTWNWMVTWNUGWHWKUB
      GVTWBGUHZWBVAUHZWNWHWKURZVTGVAUHZWOWPURABCDFGHLNOPQRVBWRWOWPGWBVCVDVEWNWP
      WQWNWPUGWCUEUHZWQWAWBVFWSWEWKWGWEWSWKWCWDUEVGVHVIVEVDVJVKVDVLVIVMVNVO $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    ${
      $d A l m n o $.  $d a l m n o $.  $d B l m n o $.  $d b c d e l m n o $.
      $d C l m n o $.  $d f l m n o $.  $d g l m n o $.  $d h l n m o $.
      $d L l m n o $.  $d x y l m n o $.  $d P l m n o $.  $d R l m n o $.
      $( Lemma for axfe (future) .  Next, we prove that ` M ` is a function. $)
      axfelem20 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> Fun M ) $=
        ( vb vl vc ve vd vm vo vn csur wss wa wcel cv cslt wbr wral cuni wfun
        wo axfelem19 sseld nofun syl6 cres wceq wrex axfelem12 abeq2i wi
        simplr anim12i simpll simpr jca32 con0 axfelem14 onelon ex syl word
        eloni ordtri2or2 syl2an simplll jca adantr simprll simprrl ad2antlr
        simprrr adantl reseq1 cin resres eqeq12i wb sseqin2 reseq2 eqeq12d
        sylbi syl5bb syl5ibcom imp axfelem17 syl112anc simpld ssres2 sseq1
        syl5ibrcom mpd simprlr orim12d exp32 com34 mpdi syl2and imp3a syl5
        anassrs expr eqeq1 eqcom syl6bb sseq2 orbi12d imbi12d biimpcd
        r19.23adva r19.23advv syl5bi r19.21aiv ralbidv jcad fununi funeqi
        sylibr ) LUIUJHUIUJUKZLCULHDULUKZUKZAUMBUMUNUOBHUPALUPZUKZEUQZURZMURUUA
        UAUMZURZUUDUBUMZUJZUUFUUDUJZUSZUBEUPZUKZUAEUPUUCUUAUUKUAEUUAUUDEULZUUEU
        UJUUAUULUUDUIULUUEUUAEUIUUDABCDEFGHIJKLMNOPQRSTUTVAUUDVBVCUULUCUMZUDUMZ
        VDZUUDVEZUEUMZUUNVDZUUDVEZUKZUDGVFZUEHVFUCLVFZUUAUUJUVBUAEEGHUDIJKLPUAU
        CUESVGVHUUAUVAUUJUCUELHUUAUUMLULUUQHULUKZUVAUUJVIUUAUVCUKUUTUUJUDGUUAUV
        CUUNGULZUUTUUJVIZUUAUVCUVDUKZUKUUOUURVEZUUOUUFUJZUUFUUOUJZUSZUBEUPZVIZU
        VEUUAUVFUVGUVKUUAUVFUVGUKZUKZUVJUBEUUFEULUFUMZUGUMZVDZUUFVEZUHUMZUVPVDZ
        UUFVEZUKZUGGVFZUHHVFUFLVFZUVNUVJUWDUBEEGHUGIJKLPUBUFUHSVGVHUVNUWCUVJUFU
        HLHUVNUVOLULUVSHULUKZUWCUVJVIUVNUWEUKUWBUVJUGGUVNUWEUVPGULZUWBUVJVIZUVN
        UWEUWFUKZUKUVQUVTVEZUUOUVQUJZUVQUUOUJZUSZVIZUWGUVNUWHUWIUWLUUAUVMUWHUWI
        UKZUWLUUAUVMUWNUKZUWLUWOUVDUWFUKZUVCUWEUKZUVGUWIUKZUKZUKUUAUWLUWOUWPUWQ
        UWRUVMUVDUWNUWFUVCUVDUVGVJUWEUWFUWIVJVKUVMUVCUWNUWEUVCUVDUVGVLUWEUWFUWI
        VLVKUVMUVGUWNUWIUVFUVGVMUWHUWIVMVKVNUUAUWPUWSUWLUUAUVDUUNVOULZUWFUVPVOU
        LZUWSUWLVIZUUAGVOULZUVDUWTVIABCDFGHLNOPQRVPZUXCUVDUWTGUUNVQVRVSUUAUXCUW
        FUXAVIUXDUXCUWFUXAGUVPVQVRVSUUAUWTUXAUKZUUNUVPUJZUVPUUNUJZUSZUXBUWTUUNV
        TUVPVTUXHUXAUUNWAUVPWAUUNUVPWBWCUUAUXEUWSUXHUWLUUAUXEUWSUXHUWLVIUUAUXEU
        WSUKZUKZUXFUWJUXGUWKUXJUXFUWJUXJUXFUKZUUOUVOUUNVDZVEZUWJUXKUXMUURUVSUUN
        VDZVEZUXKYQYTUKZUWTUWQUKZUVGUXLUXNVEZUXMUXOUKUXJUXPUXFUXJYQYTYQYRYTUXIW
        DYSYTUXIVJWEZWFUXJUXQUXFUXJUWTUWQUUAUWTUXAUWSWGUUAUXEUWQUWRWHZWEWFUXIUV
        GUUAUXFUXEUWQUVGUWIWHZWIUXJUXFUXRUXJUWIUXFUXRVIUXIUWIUUAUXEUWQUVGUWIWJZ
        WKUWIUVQUUNVDZUVTUUNVDZVEZUXFUXRUVQUVTUUNWLUYEUVOUVPUUNWMZVDZUVSUYFVDZV
        EZUXFUXRUYCUYGUYDUYHUVOUVPUUNWNUVSUVPUUNWNWOUXFUYFUUNVEZUYIUXRWPUUNUVPW
        QUYJUYGUXLUYHUXNUYFUUNUVOWRUYFUUNUVSWRWSWTXAXBVSXCABLHUUMUUQUVOUVSUUNXD
        XEXFUXFUXMUWJVIUXJUXFUWJUXMUXLUVQUJUUNUVPUVOXGUUOUXLUVQXHXIWKXJVRUXJUXG
        UWKUXJUXGUKZUUMUVPVDZUVQVEZUWKUYKUYMUUQUVPVDZUVTVEZUYKUXPUXAUWQUKZUYLUY
        NVEZUWIUYMUYOUKUXJUXPUXGUXSWFUXJUYPUXGUXJUXAUWQUUAUWTUXAUWSXKUXTWEWFUXJ
        UXGUYQUXJUVGUXGUYQVIUXIUVGUUAUYAWKUVGUUOUVPVDZUURUVPVDZVEZUXGUYQUUOUURU
        VPWLUYTUUMUUNUVPWMZVDZUUQVUAVDZVEZUXGUYQUYRVUBUYSVUCUUMUUNUVPWNUUQUUNUV
        PWNWOUXGVUAUVPVEZVUDUYQWPUVPUUNWQVUEVUBUYLVUCUYNVUAUVPUUMWRVUAUVPUUQWRW
        SWTXAXBVSXCUXIUWIUUAUXGUYBWIABLHUUMUUQUVOUVSUVPXDXEXFUXGUYMUWKVIUXJUXGU
        YLUUOUJUYMUWKUVPUUNUUMXGUYLUVQUUOXHXBWKXJVRXLXMXNXOXPXQXRXCXSXTUWMUVRUW
        AUVJUVRUWMUWAUVJVIUVRUWIUWAUWLUVJUVRUWIUUFUVTVEUWAUVQUUFUVTYAUUFUVTYBYC
        UVRUWJUVHUWKUVIUVQUUFUUOYDUVQUUFUUOXHYEYFYGXQVSXSYHVRYIYJYKXTUVLUUPUUSU
        UJUUPUVLUUSUUJVIUUPUVGUUSUVKUUJUUPUVGUUDUURVEUUSUUOUUDUURYAUUDUURYBYCUU
        PUVJUUIUBEUUPUVHUUGUVIUUHUUOUUDUUFXHUUOUUDUUFYDYEYLYFYGXQVSXSYHVRYIYJYM
        YKEUAUBYNVSMUUBTYOYP $.
        $( [12-May-2012] $) $( [22-Apr-2012] $)
    $}

    $( Lemma for axfe (future) .  Now, using the previous three lemmas, we show
       that ` M ` is a surreal. $)
    axfelem21 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> M e. No ) $=
      ( vb csur wss wa wcel cv cslt wbr wral wfun cdm con0 crn c1o c2o cpr w3a
      axfelem20 cuni axfelem18 axfelem14 onuni syl eqeltrd ciun rneqi rnuni
      eqtri axfelem19 sseld norn syl6 r19.21aiv iunss sylibr syl5eqss 3jca
      elno2 ) LUBUCHUBUCUDLCUEHDUEUDUDAUFBUFUGUHBHUIALUIUDZMUJZMUKZULUEZMUMZUNU
      OUPZUCZUQMUBUEVSVTWBWEABCDEFGHIJKLMNOPQRSTURVSWAGUSZULABCDEFGHIJKLMNOPQRS
      TUTVSGULUEWFULUEABCDFGHLNOPQRVAGVBVCVDVSWCUAEUAUFZUMZVEZWDWCEUSZUMWIMWJTV
      FUAEVGVHVSWHWDUCZUAEUIWIWDUCVSWKUAEVSWGEUEWGUBUEWKVSEUBWGABCDEFGHIJKLMNOP
      QRSTVIVJWGVKVLVMUAEWHWDVNVOVPVQMVRVO $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

  $}

  ${
    $d a f g h $.  $d a l p q $.  $d r a $.  $d f g h $.  $d K l r $.
    $d L a f g $.  $d L l p $.  $d l q r $.  $d L r $.  $d L x $.  $d M l r $.
    $d P a f g h $.  $d q p $.  $d P l r $.  $d r q $.  $d R a f g h $.
    $d R l p q $.  $d R r $.  $d R x y $.  $d a f g h b c d e t $.
    $d l r b c d e t $.  $d K b c d e t $.  $d L b c d e t $.
    $d P b c d e t $.  $d R b c d e t $.  $d p q b c d e t $.
    axfelem22.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem22.2 $e |- P = |^| D $.
    axfelem22.3 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    axfelem22.4 $e |- M = U. C $.
    $( Lemma for axfe (future) .  Given an element ` K ` of ` P ` , there are
       elements ` l ` and ` r ` of ` L ` and ` R ` whose restrictions to ` K `
       are a prefix of ` M ` . $)
    axfelem22 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> ( K e. P ->
                       ( E. l e. L ( l |` K ) C_ M /\
                         E. r e. R ( r |` K ) C_ M ) ) ) $=
      ( vd vt vb vc ve csur wss wa wcel cv cslt wbr wral con0 cres wrex wi
      axfelem14 wceq wne wn cint crab ssrab2 eqsstri onnmin mpan con2i eleq2s
      adantl onelon reseq2 eqeq12d necon3bid 2ralbidv axfelem11 elrab2 notbii
      imnan bitr4i biimpi sylc df-ne con2bii rexbii rexnal bitri sylibr eqeq1d
      anbi12d eqcom eqid biantrur biantru anbi12i anidm bitr3i syl6bb rcla4ev
      ex reximdv mpd w3a weq reseq1 anbi1d rexbidv anbi2d rcla42ev r19.40
      reximi syl cuni cvv visset resexg ax-mp eqeq2 2rexbidv axfelem12 elab2
      elssuni sylbir syl6sseqr anim12i 3syl 3expia reximdva reximia reeanv
      sylib ) MUIUJHUIUJUKMCULHDULUKUKAUMBUMUNUOBHUPAMUPUKGUQULZLGULZSUMZLURZNU
      JZSMUSOUMZLURZNUJZOHUSUKZUTABCDFGHMPQRTUAVAYOYPUUCYOYPUKZYQUDUMZURZYRVBZY
      TUUEURZYRVBZUKZUUFUUAVBZUUHUUAVBZUKZUKZUDGUSZOHUSZSMUSZUUCUUDYRUUAVBZOHUS
      ZSMUSZUUQUUDYRUUAVCZOHUPZSMUPZVDZUUTUUDLFULZVDZLUQULZUVDYPUVFYOUVFLFVEZGU
      VELUVHULZFUQUJUVEUVIVDFQUMRUMZURPUMUVJURVCPHUPQMUPZRUQVFUQTUVKRUQVGVHFLVI
      VJVKUAVLVMGLVNUVFUVGUVDUTZUVFUVGUVCUKZVDUVLUVEUVMYQUEUMZURZYTUVNURZVCZOHU
      PSMUPUVCUELUQFUVNLVBZUVQUVASOMHUVRUVOUVPYRUUAUVRUVOYRUVPUUAUVNLYQVOUVNLYT
      VOVPVQVRFHOSMPQRUETVSVTWAUVGUVCWBWCWDWEUUTUVBVDZSMUSUVDUUSUVSSMUUSUVAVDZO
      HUSUVSUURUVTOHUVAUURYRUUAWFWGWHUVAOHWIWJWHUVBSMWIWJWKYPUUTUUQUTYOYPUUSUUP
      SMYPUURUUOOHYPUURUUOUUNUURUDLGUUELVBZUUNYRYRVBZUUAYRVBZUKZUURUUAUUAVBZUKZ
      UKZUURUWAUUJUWDUUMUWFUWAUUGUWBUUIUWCUWAUUFYRYRUUELYQVOZWLUWAUUHUUAYRUUELY
      TVOZWLWMUWAUUKUURUULUWEUWAUUFYRUUAUWHWLUWAUUHUUAUUAUWIWLWMWMUWGUURUURUKUU
      RUURUWDUURUWFUURUWCUWDYRUUAWNUWBUWCYRWOWPWJUWEUURUUAWOWQWRUURWSWTXAXBXCXD
      XDVMXEUUQYSUUBUKZOHUSZSMUSUUCUUPUWKSMYQMULZUUOUWJOHUWLYTHULZUUOUWJUWLUWMU
      UOXFUFUMZUUEURZYRVBZUGUMZUUEURZYRVBZUKZUWOUUAVBZUWRUUAVBZUKZUKZUDGUSZUGHU
      SZUFMUSZUWTUDGUSZUGHUSZUFMUSZUXCUDGUSZUGHUSZUFMUSZUKZUWJUXEUUOUUGUWSUKZUU
      KUXBUKZUKZUDGUSUFUGYQYTMHUFSXGZUXDUXQUDGUXRUWTUXOUXCUXPUXRUWPUUGUWSUXRUWO
      UUFYRUWNYQUUEXHZWLXIUXRUXAUUKUXBUXRUWOUUFUUAUXSWLXIWMXJUGOXGZUXQUUNUDGUXT
      UXOUUJUXPUUMUXTUWSUUIUUGUXTUWRUUHYRUWQYTUUEXHZWLXKUXTUXBUULUUKUXTUWRUUHUU
      AUYAWLXKWMXJXLUXGUXIUXLUKZUFMUSUXNUXFUYBUFMUXFUXHUXKUKZUGHUSUYBUXEUYCUGHU
      WTUXCUDGXMXNUXHUXKUGHXMXOXNUXIUXLUFMXMXOUXJYSUXMUUBUXJYREXPZNUXJYREULYRUY
      DUJUWOUHUMZVBZUWRUYEVBZUKZUDGUSZUGHUSUFMUSZUXJUHYREYQXQULYRXQULSXRYQLXQXS
      XTUYEYRVBZUYIUXHUFUGMHUYKUYHUWTUDGUYKUYFUWPUYGUWSUYEYRUWOYAUYEYRUWRYAWMXJ
      YBEGHUDIJKMRUHUFUGUBYCZYDYREYEYFUCYGUXMUUAUYDNUXMUUAEULUUAUYDUJUYJUXMUHUU
      AEYTXQULUUAXQULOXRYTLXQXSXTUYEUUAVBZUYIUXKUFUGMHUYMUYHUXCUDGUYMUYFUXAUYGU
      XBUYEUUAUWOYAUYEUUAUWRYAWMXJYBUYLYDUUAEYEYFUCYGYHYIYJYKYLYSUUBSOMHYMYNXOX
      CXO $.
      $( [12-May-2012] $) $( [23-Apr-2012] $)
  $}

  $(
  @{
     axfelem23.1 @e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } @.
     axfelem23.2 @e |- P = |^| D @.
     axfelem23.3 @e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } @.
     axfelem23.4 @e |- M = U. C @.

     @( Lemma for axfe (future) . There are elements of ` L ` and ` R `
        whose restrictions to ` U. P ` are ` M ` . @)
     axfelem23 @p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) ->
                       ( E. l e. L ( l |` U. P ) = M /\
                         E. r e. R ( r |` U. P ) = M ) ) @= ? @.
  @}
  $)

