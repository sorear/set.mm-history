$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Full-Eta Property
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( Lemma for axfe (future) .  The successor of the union of the image of the
     birthday function under a set is an ordinal. $)
  axfelem1 $p |- ( A e. V -> suc U. ( bday " A ) e. On ) $=
    ( wcel cbday cima cuni word wa csuc con0 wfun bdayfun funimaexg mpan uniexg
    cvv syl wss crn imassrn bdayrn sseqtri ssorduni ax-mp jctil sucelon bitr3i
    elon2 sylib ) ABCZDAEZFZGZULPCZHZULIJCZUJUNUMUJUKPCZUNDKUJUQLDABMNUKPOQUKJR
    UMUKDSJDATUAUBUKUCUDUEUOULJCUPULUHULUFUGUI $.
    $( [10-Sep-2011] $) $( [20-Aug-2011] $)

  ${
    $d F a b n $.  $d X a b $.
    axfelem2.1 $e |- X e. { 1o , 2o } $.
    axfelem2.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Show a particular abstraction is an
       ordinal. $)
    axfelem2 $p |- ( ( F C_ No /\ F e. A ) -> C e. On ) $=
      ( csur wss wcel cv cfv wne wrex con0 cbday c0 wceq wa wral crab cint cima
      cuni csuc axfelem1 cdm ssel2 bdaydm syl6eleqr simpr wfun bdayfun funfvima
      wi mpan sylc elssuni syl word wb bdayelon imassrn bdayrn sseqtri ssorduni
      crn ax-mp ordsssuc mp2an sylib nosgnn0i axdenselem2 neeq1d mpbiri rcla4ev
      fveq2 syl2anc ralrimiva rexeq ralbidv syl2an ancoms onintrab2 syl5eqel )
      DJKZDALZUAZBGMZCMZNZEOZGFMZPZCDUBZFQUCUDZQIWJWQFQPZWRQLWIWHWSWIRDUEZUFZUG
      ZQLWNGXBPZCDUBZWSWHDAUHWHXCCDWHWLDLZUAZWLRNZXBLZXGWLNZEOZXCXFXGXAKZXHXFXG
      WTLZXKXFWLRUIZLZXEXLXFWLJXMDJWLUJZUKULWHXEUMRUNXNXEXLUQUODWLRUPURUSXGWTUT
      VAXGQLXAVBZXKXHVCWLVDWTQKXPWTRVIQRDVEVFVGWTVHVJXGXAVKVLVMXFXJSEOEHVNXFXIS
      EXFWLJLXISTXOWLVOVAVPVQWNXJGXGXBWKXGTWMXIEWKXGWLVSVPVRVTWAWQXDFXBQWOXBTWP
      XCCDWNGWOXBWBWCVRWDWEWQFWFVMWG $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

    $( Lemma for axfe (future) .  Calculate the birthday of
       ` ( C X. { X } ) ` . $)
    axfelem3 $p |- ( ( F C_ No /\ F e. A ) ->
                       ( bday ` ( C X. { X } ) ) = C ) $=
      ( csur wss wcel wa csn cxp cbday cfv cdm con0 wceq noxpsgn bdayval c0 wne
      axfelem2 3syl c1o c2o cpr elexi snnz dmxp ax-mp syl6eq ) DJKDALMZBENZOZPQ
      ZUQRZBUOBSLUQJLURUSTABCDEFGHIUEBEHUAUQUBUFUPUCUDUSBTEEUGUHUIHUJUKBUPULUMU
      N $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

  $}

  ${
    $d A x y $.  $d X x y $.
    axfelem4.1 $e |- X e. { 1o , 2o } $.
    $( Lemma for axfe (future) .  The infimum of the class of all ordinals such
       that ` A ` is not ` X ` is an ordinal. $)
    axfelem4 $p |- ( A e. No ->
                        |^| { x e. On | ( A ` x ) =/= X } e. On ) $=
      ( csur wcel cv cfv wne con0 wrex crab cbday bdayelon nosgnn0i axdenselem2
      cint c0 neeq1d mpbiri wceq fveq2 rcla4ev sylancr onintrab2 sylib ) BEFZAG
      ZBHZCIZAJKZUJAJLQJFUGBMHZJFULBHZCIZUKBNUGUNRCICDOUGUMRCBPSTUJUNAULJUHULUA
      UIUMCUHULBUBSUCUDUJAUEUF $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)

    $( Lemma for axfe (future) .  There is always a minimal value of a surreal
       that is not a given sign. $)
    axfelem5 $p |- ( A e. No ->
                      ( A ` |^| { x e. On | ( A ` x ) =/= X } ) =/= X ) $=
      ( vy csur wcel cv cfv wne con0 crab cint wa wss neeq1d wceq fveq2 ax-17
      c0 ssrab2 cbday nosgnn0i axdenselem2 mpbiri wb bdayelon elrab3 ax-mp ne0i
      sylibr syl onint sylancr hbrab1 hbint hbfv hbne elrabf sylib simprd ) BFG
      ZAHZBIZCJZAKLZMZKGZVGBIZCJZVBVGVFGZVHVJNVBVFKOVFTJZVKVEAKUAVBBUBIZVFGZVLV
      BVMBIZCJZVNVBVPTCJCDUCVBVOTCBUDPUEVMKGVNVPUFBUGVEVPAVMKVCVMQVDVOCVCVMBRPU
      HUIUKVFVMUJULVFUMUNVEVJAEVGKAEVFVEAEKUOUPZEHZKGASAEVICAEVGBVRBGASVQUQVRCG
      ASURVCVGQVDVICVCVGBRPUSUTVA $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A a b x $.  $d F a b $.  $d X a b x $.  $d X n $.  $d a b n $.
    $d A n $.  $d F n $.
    axfelem6.1 $e |- X e. { 1o , 2o } $.
    axfelem6.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Given an element ` A ` of ` F ` , then the
       first position where it differs from ` X ` is strictly less than
       ` C ` $)
    axfelem6 $p |- ( ( F C_ No /\ A e. F ) ->
                     |^| { x e. On | ( A ` x ) =/= X } e. C ) $=
      ( csur wss wcel wa cv cfv wne con0 wrex neeq1d crab cint wral wi bdayelon
      cbday ssel2 c0 nosgnn0i axdenselem2 mpbiri syl wceq fveq2 rcla4ev sylancr
      onintrab2 sylib rexbidv rcla4v ad2antlr wel ad2antrr simplr onelon anim1i
      fveq1 anasss weq elrab intss1 sylbir adantll simprl imp syl22anc rexlimdv
      ontr2 exp32 syld ralrimiva elintrabg biimpar syl2anc syl6eleqr ) EKLZBEMZ
      NZAOZBPZFQZARUAZUBZHOZDOZPZFQZHGOZSZDEUCZGRUAUBZCWHWMRMZWTWMWRMZUDZGRUCZW
      MXAMZWHWKARSZXBWHBUFPZRMXHBPZFQZXGBUEWHBKMZXJEKBUGXKXJUHFQFIUIXKXIUHFBUJT
      UKULWKXJAXHRWIXHUMWJXIFWIXHBUNTUOUPWKAUQURZWHXDGRWHWRRMZNZWTWNBPZFQZHWRSZ
      XCWGWTXQUDWFXMWSXQDBEWOBUMZWQXPHWRXRWPXOFWNWOBVGTUSUTVAXNXPXCHWRXNHGVBZXP
      XCXNXSXPNZNXBXMWMWNLZXSXCWHXBXMXTXLVCWHXMXTVDXMXTYAWHXMXTNWNRMZXPNZYAXMXS
      XPYCXMXSNYBXPWRWNVEVFVHYCWNWLMYAWKXPAWNRAHVIWJXOFWIWNBUNTVJWNWLVKVLULVMXN
      XSXPVNXBXMNYAXSNXCWMWNWRVRVOVPVSVQVTWAXBXFXEWTGWMRRWBWCWDJWE $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d A a b x $.  $d F a b $.  $d X a b x $.  $d a b n $.  $d A n $.
    $d F n $.  $d X n $.
    axfelem7.1 $e |- X e. { 1o , 2o } $.
    axfelem7.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= X } $.
    $( Lemma for axfe (future) .  Calculate the value of ` ( C X. { X } ) ` at
       the minimal ordinal whose value is different from ` X ` . $)
    axfelem7 $p |- ( ( F C_ No /\ A e. F ) ->
                      ( ( C X. { X } ) ` |^| { x e. On | ( A ` x ) =/= X } ) =
                      X ) $=
      ( csur wss wcel wa cv cfv wne con0 crab cint csn cxp axfelem6 c1o c2o cpr
      wceq elexi fvconst2 syl ) EKLBEMNAOBPFQARSTZCMUKCFUAUBPFUGABCDEFGHIJUCCFU
      KFUDUEUFIUHUIUJ $.
      $( [10-Sep-2011] $) $( [3-Aug-2011] $)
  $}

  ${
    $d F a b n $.  $d S a b $.  $d X a b n $.
    axfelem8.1 $e |- S e. { 1o , 2o } $.
    axfelem8.2 $e |- C = |^| { a e. On |
                                A. n e. F E. b e. a ( n ` b ) =/= S } $.
    $( Lemma for axfe (future) .  Bound the birthday of ` ( C X. { X } ) `
       above. $)
    axfelem8 $p |- ( ( ( F C_ No /\ F e. A ) /\
                          ( X e. On /\ A. n e. F ( bday ` n ) e. X ) ) ->
                        ( bday ` ( C X. { S } ) ) C_ X ) $=
      ( csur wss wcel wa con0 cv cbday cfv wral wne csn wrex crab cint wi ssel2
      cxp c0 nosgnn0i axdenselem2 neeq1d mpbiri syl wceq fveq2 rcla4ev ralimdva
      expcom anim2d rexeq ralbidv elrab intss1 sylbir syl6 imp syl5eqss adantlr
      wb axfelem3 sseq1d adantr mpbird ) EKLZEAMZNZFOMZDPZQRZFMZDESZNZNBCUAUGQR
      ZFLZBFLZVNWBWEVOVNWBNBHPZVRRZCTZHGPZUBZDESZGOUCZUDZFJVNWBWMFLZVNWBVQWHHFU
      BZDESZNZWNVNWAWPVQVNVTWODEVNVREMNZVSVRRZCTZVTWOUEWRVRKMZWTEKVRUFXAWTUHCTC
      IUIXAWSUHCVRUJUKULUMVTWTWOWHWTHVSFWFVSUNWGWSCWFVSVRUOUKUPURUMUQUSWQFWLMWN
      WKWPGFOWIFUNWJWODEWHHWIFUTVAVBFWLVCVDVEVFVGVHVPWDWEVIWBVPWCBFABDECGHIJVJV
      KVLVM $.
      $( [10-Sep-2011] $) $( [17-Aug-2011] $)
  $}

  ${
    $d L a b c d k n $.  $d A d x $.  $d a n x z $.  $d b x $.  $d b z $.
    $d c x $.  $d k x $.  $d L x $.  $d L z $.  $d R n $.  $d R y $.  $d R z $.
    $d X a $.  $d X b $.  $d X n $.  $d X z $.

    $( Lemma for axfe (future) .  The full statement of the axiom when ` R ` is
       empty. $)
    axfelem9 $p |- ( R = (/) ->
                      ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                          ( X e. On /\ A. n e. ( L u. R ) ( bday ` n ) e. X )
        /\
                          A. x e. L A. y e. R x <s y ) ->
                        E. z e. No ( ( bday ` z ) C_ X /\
                                     A. x e. L x <s z /\
                                     A. y e. R z <s y ) ) ) $=
      ( vk c0 wceq csur wa wcel con0 cv cfv wral c2o vb va vd vc wss cbday cslt
      cun wbr w3a wrex wi wne crab cint csn cxp c1o 2on elexi prid2 eqid noxp2o
      axfelem2 syl adantr axfelem8 cop ctp ssel2 adantlr axfelem4 wn onss sseld
      wb imp ontri1 syl2anc biimpd con2d ex pm2.43d intss1 nsyl df-ne weq fveq2
      neeq1d elrab3 biimprd mt3d axfelem6 onelss sylc fvconst2 eqtr4d ralrimiva
      syl5bir w3o wo axfelem5 sylib nofv 3orel3 orcomd jctir olcd 3orass sylibr
      andir fvex brtp axfelem7 breqtrrd breq12d anbi12d rcla4ev syl12anc sltval
      raleq biimpar syl21anc sseq1d breq2 ralbidv ad2ant2r 3adant3 uneq2 syl6eq
      0ex un0 raleqdv anbi2d 3anbi2d 3anbi3d df-3an ral0 mpbiran2 syl6bb mpbiri
      rexbidv imbi12d ) FKLZHMUEZFMUEZNHDOZFEOZNNZIPOZGQZUFRIOZGHFUHZSZNZAQZBQZ
      UGUIBFSAHSZUJZCQZUFRZIUEZUUPUUTUGUIZAHSZUUTUUQUGUIZBFSZUJZCMUKZULUUIUUJUU
      LGHSZNZUURUJZUVBUVDNZCMUKZULUUIUVJUVMUURUUIUVJUVMUUEUUGUVJUVMULUUFUUHUUEU
      UGNZUVJUVMUVNUVJNUAQUUKRTUMUAUBQUKGHSUBPUNUOZTUPUQZMOZUVPUFRZIUEZUUPUVPUG
      UIZAHSZUVMUVNUVQUVJUVNUVOPOZUVQDUVOGHTUBUAURTTPUSUTZVAZUVOVBZVDZUVOVCVEZV
      FDUVOTGHIUBUAUWDUWEVGUVNUWAUVJUVNUVTAHUVNUUPHOZNZUUPMOZUVQUCQZUUPRZUWKUVP
      RZLZUCUDQZSZUWOUUPRZUWOUVPRZURKVHURTVHKTVHVIZUIZNZUDPUKZUVTUUEUWHUWJUUGHM
      UUPVJVKZUVNUVQUWHUWGVFUWIJQZUUPRZTUMZJPUNZUOZPOZUWNUCUXHSZUXHUUPRZUXHUVPR
      ZUWSUIZUXBUWIUWJUXIUXCJUUPTUWDVLVEZUWIUWNUCUXHUWIUWKUXHOZNZUWLTUWMUXPUWLT
      LZUWKUXGOZUXPUXHUWKUEZUXRUWIUXOUXSVMZUWIUXOUXTUWIUXOUXOUXTULUXPUXSUXOUXPU
      XSUXOVMZUXPUXIUWKPOZUXSUYAVPUWIUXIUXOUXNVFUWIUXOUYBUWIUXHPUWKUWIUXIUXHPUE
      UXNUXHVNVEVOVQZUXHUWKVRVSVTWAWBWCVQUWKUXGWDWEUXQVMUWLTUMZUXPUXRUWLTWFUXPU
      YBUYDUXRULUYCUYBUXRUYDUXFUYDJUWKPJUCWGUXEUWLTUXDUWKUUPWHWIWJWKVEWSWLUXPUW
      KUVOOZUWMTLUWIUXOUYEUWIUXHUVOUWKUWIUWBUXHUVOOZUXHUVOUEUVNUWBUWHUWFVFUUEUW
      HUYFUUGJUUPUVOGHTUBUAUWDUWEWMVKUVOUXHWNWOVOVQUVOTUWKUWCWPVEWQWRUWIUXKTUXL
      UWSUWIUXKURLZTKLNZUYGTTLZNZUXKKLZUYINZWTZUXKTUWSUIUWIUYHUYJUYLXAZXAUYMUWI
      UYNUYHUWIUYGUYKXAZUYINUYNUWIUYOUYIUWIUYKUYGUWIUXKTLZVMZUYKUYGUYPWTZUYKUYG
      XAUWIUXKTUMZUYQUWIUWJUYSUXCJUUPTUWDXBVEUXKTWFXCUWIUWJUYRUXCUUPUXHXDVEUYKU
      YGUYPXEWOXFTVBXGUYGUYKUYIXKXCXHUYHUYJUYLXIXJURKURTKTUXKTUXHUUPXLUWCYKUWCU
      WCXMXJUUEUWHUXLTLUUGJUUPUVOGHTUBUAUWDUWEXNVKXOUXAUXJUXMNUDUXHPUWOUXHLZUWP
      UXJUWTUXMUWNUCUWOUXHYAUYTUWQUXKUWRUXLUWSUWOUXHUUPWHUWOUXHUVPWHXPXQXRXSUWJ
      UVQNUVTUXBUDUCUUPUVPXTYBYCWRVFUVLUVSUWANCUVPMUUTUVPLZUVBUVSUVDUWAVUAUVAUV
      RIUUTUVPUFWHYDVUAUVCUVTAHUUTUVPUUPUGYEYFXQXRXSWBYGVQYHUUDUUSUVKUVHUVMUUDU
      UOUVJUUIUURUUDUUNUVIUUJUUDUULGUUMHUUDUUMHKUHHFKHYIHYLYJYMYNYOUUDUVGUVLCMU
      UDUVGUVBUVDUVEBKSZUJZUVLUUDUVFVUBUVBUVDUVEBFKYAYPVUCUVLVUBUVBUVDVUBYQUVEB
      YRYSYTUUBUUCUUA $.
      $( [4-Apr-2012] $) $( [3-Aug-2011] $)
  $}

  ${
    $d a b c d k n y z $.  $d B d y $.  $d L n z $.  $d L x $.
    $d R a b c d n y z $.  $d X a b n z $.
    $( Lemma for axfe (future) .  The full statement of the axiom when ` L ` is
       empty. $)
    axfelem10 $p |- ( L = (/) ->
                      ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                          ( X e. On /\ A. n e. ( L u. R ) ( bday ` n ) e. X )
        /\
                          A. x e. L A. y e. R x <s y ) ->
                        E. z e. No ( ( bday ` z ) C_ X /\
                                     A. x e. L x <s z /\
                                     A. y e. R z <s y ) ) ) $=
      ( vk c0 wceq csur wa wcel con0 cv cfv wral c1o vb va vd vc wss cbday cslt
      cun wbr w3a wrex wi wne crab cint csn cxp c2o 1on elexi prid1 eqid noxp1o
      axfelem2 syl adantr axfelem8 cop ctp ssel2 adantlr axfelem4 axfelem6 sylc
      onelss sseld imp fvconst2 wn wb onss ontri1 syl2anc biimpd pm2.43d intss1
      con2d nsyl df-ne weq fveq2 neeq1d elrab3 biimprd syl5bir eqtr4d ralrimiva
      ex mt3d axfelem7 w3o wo axfelem5 sylib nofv 3orel2 jctil andi orcd df-3or
      sylibr fvex 0ex 2on eqbrtrd raleq breq12d anbi12d rcla4ev syl12anc sltval
      brtp biimpar syl21anc sseq1d breq1 ralbidv ad2ant2l uneq1 uncom un0 eqtri
      3adant3 syl6eq raleqdv anbi2d 3anbi2d 3anass ral0 biantrur anbi2i rexbidv
      bitr4i syl6bb imbi12d mpbiri ) HKLZHMUEZFMUEZNHDOZFEOZNNZIPOZGQZUFRIOZGHF
      UHZSZNZAQZBQZUGUIBFSAHSZUJZCQZUFRZIUEZUUSUVCUGUIZAHSZUVCUUTUGUIZBFSZUJZCM
      UKZULUULUUMUUOGFSZNZUVAUJZUVEUVINZCMUKZULUULUVMUVPUVAUULUVMUVPUUIUUKUVMUV
      PULUUHUUJUUIUUKNZUVMUVPUVQUVMNUAQUUNRTUMUAUBQUKGFSUBPUNUOZTUPUQZMOZUVSUFR
      ZIUEZUVSUUTUGUIZBFSZUVPUVQUVTUVMUVQUVRPOZUVTEUVRGFTUBUATURTPUSUTZVAZUVRVB
      ZVDZUVRVCVEZVFEUVRTGFIUBUAUWGUWHVGUVQUWDUVMUVQUWCBFUVQUUTFOZNZUVTUUTMOZUC
      QZUVSRZUWNUUTRZLZUCUDQZSZUWRUVSRZUWRUUTRZTKVHTURVHKURVHVIZUIZNZUDPUKZUWCU
      VQUVTUWKUWJVFUUIUWKUWMUUKFMUUTVJVKZUWLJQZUUTRZTUMZJPUNZUOZPOZUWQUCUXKSZUX
      KUVSRZUXKUUTRZUXBUIZUXEUWLUWMUXLUXFJUUTTUWGVLVEZUWLUWQUCUXKUWLUWNUXKOZNZU
      WOTUWPUXSUWNUVROZUWOTLUWLUXRUXTUWLUXKUVRUWNUWLUWEUXKUVROZUXKUVRUEUVQUWEUW
      KUWIVFUUIUWKUYAUUKJUUTUVRGFTUBUAUWGUWHVMVKUVRUXKVOVNVPVQUVRTUWNUWFVRVEUXS
      UWPTLZUWNUXJOZUXSUXKUWNUEZUYCUWLUXRUYDVSZUWLUXRUYEUWLUXRUXRUYEULUXSUYDUXR
      UXSUYDUXRVSZUXSUXLUWNPOZUYDUYFVTUWLUXLUXRUXQVFUWLUXRUYGUWLUXKPUWNUWLUXLUX
      KPUEUXQUXKWAVEVPVQZUXKUWNWBWCWDWGWRWEVQUWNUXJWFWHUYBVSUWPTUMZUXSUYCUWPTWI
      UXSUYGUYIUYCULUYHUYGUYCUYIUXIUYIJUWNPJUCWJUXHUWPTUXGUWNUUTWKWLWMWNVEWOWSW
      PWQUWLUXNTUXOUXBUUIUWKUXNTLUUKJUUTUVRGFTUBUAUWGUWHWTVKUWLTTLZUXOKLZNZUYJU
      XOURLZNZTKLUYMNZXAZTUXOUXBUIUWLUYLUYNXBZUYOXBUYPUWLUYQUYOUWLUYJUYKUYMXBZN
      UYQUWLUYRUYJUWLUXOTLZVSZUYKUYSUYMXAZUYRUWLUXOTUMZUYTUWLUWMVUBUXFJUUTTUWGX
      CVEUXOTWIXDUWLUWMVUAUXFUUTUXKXEVEUYKUYSUYMXFVNTVBXGUYJUYKUYMXHXDXIUYLUYNU
      YOXJXKTKTURKURTUXOUWFUXKUUTXLXMURPXNUTZVUCYBXKXOUXDUXMUXPNUDUXKPUWRUXKLZU
      WSUXMUXCUXPUWQUCUWRUXKXPVUDUWTUXNUXAUXOUXBUWRUXKUVSWKUWRUXKUUTWKXQXRXSXTU
      VTUWMNUWCUXEUDUCUVSUUTYAYCYDWQVFUVOUWBUWDNCUVSMUVCUVSLZUVEUWBUVIUWDVUEUVD
      UWAIUVCUVSUFWKYEVUEUVHUWCBFUVCUVSUUTUGYFYGXRXSXTWRYHVQYMUUGUVBUVNUVKUVPUU
      GUURUVMUULUVAUUGUUQUVLUUMUUGUUOGUUPFUUGUUPKFUHZFHKFYIVUFFKUHFKFYJFYKYLYNY
      OYPYQUUGUVJUVOCMUUGUVJUVEUVFAKSZUVIUJZUVOUUGUVGVUGUVEUVIUVFAHKXPYQVUHUVEV
      UGUVINZNUVOUVEVUGUVIYRUVIVUIUVEVUGUVIUVFAYSYTUUAUUCUUDUUBUUEUUF $.
      $( [4-Apr-2012] $) $( [3-Aug-2011] $)
  $}

  ${
    $d a b m n $.  $d p a q $.  $d m b n $.  $d p b q $.  $d L a b n p $.
    $d m n q $.  $d n p q $.  $d R a b m n $.  $d R p q $.
    axfelem11.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    $( Lemma for axfe (future) .  We are now going to construct a new surreal
       for the case where ` L ` and ` R ` are not null.  The birthday/domain of
       this surreal will be the intersection of ` D ` .  This lemma just
       changes bound variables for later use. $)
    axfelem11 $p |- D = { b e. On | A. n e. L A. m e. R
                            ( n |` b ) =/= ( m |` b ) } $=
      ( cv cres wne wral con0 crab wceq reseq2 neeq12d reseq1 2ralbidv cbvral2v
      neeq1d neeq2d syl6bb cbvrabv eqtri ) AGKZHKZLZFKZUILZMZFBNGENZHOPDKZIKZLZ
      CKZUPLZMZCBNDENZIOPJUNVAHIOUIUPQZUNUHUPLZUKUPLZMZFBNGENVAVBUMVEGFEBVBUJVC
      ULVDUIUPUHRUIUPUKRSUAVEUTUQVDMGFDCEBUHUOQVCUQVDUHUOUPTUCUKURQVDUSUQUKURUP
      TUDUBUEUFUG $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)
  $}

  ${
    $d a b c d e f $.  $d g h a b c $.  $d h d $.  $d f g h $.  $d L b c f g $.
    $d P a b c d e f $.  $d P g h $.  $d R b c d f $.  $d R g h $.
    axfelem12.1 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                      ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    $( Lemma for axfe (future) .  The surreal mentioned in the previous lemma
       will ultimately be the union of ` C ` .  Again, we just change bound
       variables for later. $)
    axfelem12 $p |- C = { b | E. c e. L E. d e. R E. e e. P
                     ( ( c |` e ) = b /\ ( d |` e ) = b ) } $=
      ( cv cres wceq wa wrex eqeq1d cbvrexv cab anbi12d rexbidv 2rexbidv reseq1
      eqeq2 anbi1d anbi2d reseq2 rexbii bitri syl6bb cbvabv eqtri ) AFNZINZOZEN
      ZPZGNZUPOZURPZQZIBRZGCRFHRZEUAKNZDNZOZJNZPZLNZVGOZVIPZQZDBRZLCRZKHRZJUAMV
      EVQEJURVIPZVEUQVIPZVAVIPZQZIBRZGCRZFHRZVQVRVDWBFGHCVRVCWAIBVRUSVSVBVTURVI
      UQUFURVIVAUFUBUCUDWDVFUPOZVIPZVTQZIBRZGCRZKHRVQWCWIFKHUOVFPZWAWGGICBWJVSW
      FVTWJUQWEVIUOVFUPUESUGUDTWIVPKHWIWFVKUPOZVIPZQZIBRZLCRVPWHWNGLCUTVKPZWGWM
      IBWOVTWLWFWOVAWKVIUTVKUPUESUHUCTWNVOLCWMVNIDBUPVGPZWFVJWLVMWPWEVHVIUPVGVF
      UISWPWKVLVIUPVGVKUISUBTUJUKUJUKULUMUN $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

  $}

  $( Lemma for axfe (future) .  Establish an equality result for restriction of
     surreals. $)
  axfelem13 $p |- ( ( A C_ No /\ X e. A ) ->
                    ( X |` suc U. ( bday " ( A u. B ) ) ) = X ) $=
    ( csur wss wcel wa wrel cdm cbday cima cuni csuc cres wceq ssel2 wfun nofun
    cun syl funrel cfv bdayval bdaydm syl6eleqr bdayfun jctil wi funfvima com12
    adantl mpd ssun1 imass2 ax-mp sseli elssuni sssucid sstr2 eqsstr3d relssres
    mpi syl2anc ) ADEZCAFZGZCHZCIZJABSZKZLZMZECVLNCOVFCDFZVGADCPZVMCQVGCRCUATTV
    FVHCJUBZVLVFVMVOVHOVNCUCTVFVOVKEZVOVLEZVFVOVJFZVPVFVOJAKZFZVRVFJQZCJIZFZGZV
    TVFWCWAVFCDWBVNUDUEUFUGVEWDVTUHVDWDVEVTACJUIUJUKULVSVJVOAVIEVSVJEABUMAVIJUN
    UOUPTVOVJUQTVPVKVLEVQVKURVOVKVLUSVBTUTCVLVAVC $.
    $( [12-May-2012] $) $( [22-Apr-2012] $)

  ${
    $d p a q $.  $d L a p $.  $d L x $.  $d R a p q $.  $d R x y $.
    $d p a q b m n $.  $d L b m n $.  $d R b m n $.  $d x y b m n $.
    axfelem14.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem14.2 $e |- P = |^| D $.
    $( Lemma for axfe (future) .  Now, we prove that ` P ` , the birthday of a
       surreal we are going to be working with, is actually an ordinal. $)
    axfelem14 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\
                           ( L e. A /\ R e. B ) ) /\
                          A. x e. L A. y e. R x <s y ) -> P e. On ) $=
      ( vn vb vm wa wcel cslt con0 csur wss cv wbr wral cres wne wrex cbday cun
      cima cuni csuc cvv unexg axfelem1 syl ad2antlr breq1 breq2 rcla42v impcom
      wi wceq ssel2 sltirr notbid syl5ibcom necon2ad ad2ant2r axfelem13 imaeq2i
      wn uncom unieqi suceq ax-mp reseq2i ad2ant2l eqeq12d necon3bid sylibrd ex
      syl5eq com23 syl5 expdimp pm2.43d adantlr reseq2 2ralbidv rcla4ev syl2anc
      ralrimivv crab cint axfelem11 inteqi eqtri eleq1i onintrab2 bitr4i sylibr
      ) HUAUBZGUAUBZQZHCRGDRQZQAUCZBUCZSUDZBGUEAHUEZQZNUCZOUCZUFZPUCZXNUFZUGZPG
      UENHUEZOTUHZFTRZXLUIHGUJZUKZULZUMZTRZXMYEUFZXPYEUFZUGZPGUENHUEZXTXGYFXFXK
      XGYBUNRYFHGCDUOYBUNUPUQURXFXKYJXGXFXKQZYINPHGYKXMHRZXPGRZQZYIXFXKYNYNYIVC
      ZXKYNQXMXPSUDZXFYOYNXKYPXJYPXMXISUDABXMXPHGXHXMXISUSXIXPXMSUTVAVBXFYNYPYI
      XFYNYPYIVCXFYNQZYPXMXPUGZYIXDYLYPYRVCXEYMXDYLQZYPXMXPYSXMXMSUDZVMZXMXPVDZ
      YPVMYSXMUARUUAHUAXMVEXMVFUQUUBYTYPXMXPXMSUTVGVHVIVJYQYGYHXMXPYQYGXMYHXPXD
      YLYGXMVDXEYMHGXMVKVJXEYMYHXPVDXDYLXEYMQYHXPUIGHUJZUKZULZUMZUFXPYEUUFXPYDU
      UEVDYEUUFVDYCUUDYBUUCUIHGVNVLVOYDUUEVPVQVRGHXPVKWDVSVTWAWBWCWEWFWGWHWNWIX
      SYJOYETXNYEVDZXRYINPHGUUGXOXQYGYHUUGXOYGXQYHXNYEXMWJXNYEXPWJVTWAWKWLWMYAX
      SOTWOZWPZTRXTFUUITFEWPUUIMEUUHEGPNHIJKOLWQWRWSWTXSOXAXBXC $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    $( Lemma for axfe (future) .  For non-empty ` L ` and ` R ` , ` P ` is
       non-empty. $)
    axfelem15 $p |- ( ( L =/= (/) /\ R =/= (/) ) -> P =/= (/) ) $=
      ( vn vb vm c0 wne wa cres wral con0 wcel wex cv crab wn wceq eqidd 2eximi
      wrex ancli r2ex sylibr n0 anbi12i eeanv bitr4i df-ne con2bii rexbii bitri
      rexnal bitr2i 3imtr4i reseq2 res0 syl6eq neeq12d 2ralbidv elrab nsyl cint
      simprbi axfelem11 inteqi eqtri neeq1i wss ssrab2 onint0 ax-mp necon3abii
      wb ) DMNZCMNZOZMJUAZKUAZPZLUAZWEPZNZLCQJDQZKRUBZSZUCZBMNZWCMMNZLCQZJDQZWL
      WDDSZWGCSZOZLTJTZMMUDZLCUGZJDUGZWCWQUCZXAWTXBOZLTJTXDWTXFJLWTXBWTMUEUHUFX
      BJLDCUIUJWCWRJTZWSLTZOXAWAXGWBXHJDUKLCUKULWRWSJLUMUNXDWPUCZJDUGXEXCXIJDXC
      WOUCZLCUGXIXBXJLCWOXBMMUOUPUQWOLCUSURUQWPJDUSUTVAWLMRSWQWJWQKMRWEMUDZWIWO
      JLDCXKWFMWHMXKWFWDMPMWEMWDVBWDVCVDXKWHWGMPMWEMWGVBWGVCVDVEVFVGVJVHWNWKVIZ
      MNWMBXLMBAVIXLIAWKACLJDEFGKHVKVLVMVNWLXLMWKRVOXLMUDWLVTWJKRVPWKVQVRVSURUJ
      $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d Q y $.  $d R x y $.
    $( Lemma for axfe (future) .  Preliminary work for ~ axfelem17 . $)
    axfelem16 $p |- ( ( ( ( A C_ No /\ B C_ No ) /\
                           A. x e. A A. y e. B x <s y ) /\
                         ( X e. On /\
                           ( ( P e. A /\ Q e. B ) /\
                             ( R e. A /\ S e. B ) ) ) /\
                         ( ( P |` X ) = ( Q |` X ) /\
                           ( R |` X ) = ( S |` X ) ) ) ->
                       ( -. ( P |` X ) <s ( R |` X ) /\
                         -. ( Q |` X ) <s ( S |` X ) ) ) $=
      ( csur wa cslt wbr wcel cres wn adantl jca adantr wi cv wral con0 simplll
      wss wceq simprrl sseldd simpllr simprlr breq1 rcla42v sylc sltasym biimpd
      breq2 simprl sltres syl3anc sylan9r mtod biimprd 3impa ) CJUEZDJUEZKZAUAZ
      BUAZLMZBDUBACUBZKZIUCNZECNZFDNZKZGCNZHDNZKZKZKZEIOZFIOZUFZGIOZHIOZUFZKZWA
      WDLMZPZWBWELMZPZKVKVTKZWGKZWIWKWMWHFGLMZWMGJNZFJNZKZGFLMZWNPWLWQWGWLWOWPW
      LCJGVDVEVJVTUDVTVPVKVLVOVPVQUGQZUHZWLDJFVDVEVJVTUIVTVNVKVLVMVNVRUJQZUHZRS
      WMVPVNKZVJWRWLXCWGWLVPVNWSXARSVFVJVTWGUIVIWRGVHLMABGFCDVGGVHLUKVHFGLUPULU
      MGFUNUMZWGWHWBWDLMZWLWNWCWHXETWFWCWHXEWAWBWDLUKUOSWLWPWOVLXEWNTXBWTVKVLVS
      UQFGIURUSZUTVAWMWJWNXDWGWJXEWLWNWFWJXETWCWFXEWJWDWEWBLUPVBQXFUTVARVC $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    ${
      $d P x y $.  $d S y $.
      $( Lemma for axfe (future) .  A uniqueness result for restrictions of
         surreals. $)
      axfelem17 $p |- ( ( ( ( A C_ No /\ B C_ No ) /\
                           A. x e. A A. y e. B x <s y ) /\
                         ( X e. On /\
                           ( ( P e. A /\ Q e. B ) /\
                             ( R e. A /\ S e. B ) ) ) /\
                         ( ( P |` X ) = ( Q |` X ) /\
                           ( R |` X ) = ( S |` X ) ) ) ->
                       ( ( P |` X ) = ( R |` X ) /\
                         ( Q |` X ) = ( S |` X ) ) ) $=
        ( csur wa cslt wbr wcel cres wceq wn 3ad2ant2 sseldd noreson wss cv w3a
        wral con0 axfelem16 simp1 simp2l simp2rr simp2rl jca32 simp3r syl112anc
        simp3l jca an4 sylib wb simp1ll simprll syl2anc simprrl simp1lr simprlr
        simprrr slttrieq2 bi2anan9 syl22anc mpbird ) CJUAZDJUAZKAUBBUBLMBDUDACU
        DZKZIUENZECNZFDNZKZGCNZHDNZKZKZKZEIOZFIOZPZGIOZHIOZPZKZUCZWCWFPZWDWGPZK
        ZWCWFLMQZWFWCLMQZKZWDWGLMQZWGWDLMQZKZKZWJWNWQKZWOWRKZKWTWJXAXBABCDEFGHI
        UFWJVMVNVTVQKKWHWEXBVMWBWIUGWJVNVTVQVMVNWAWIUHZVQVTVNVMWIUIVQVTVNVMWIUJ
        UKVMWBWEWHULVMWBWEWHUNABCDGHEFIUFUMUOWNWQWOWRUPUQWJWCJNZWFJNZWDJNZWGJNZ
        WMWTURWJEJNVNXDWJCJEVJVKVLWBWIUSZWBVMVOWIVNVOVPVTUTRSXCEITVAWJGJNVNXEWJ
        CJGXHWBVMVRWIVNVQVRVSVBRSXCGITVAWJFJNVNXFWJDJFVJVKVLWBWIVCZWBVMVPWIVNVO
        VPVTVDRSXCFITVAWJHJNVNXGWJDJHXIWBVMVSWIVNVQVRVSVERSXCHITVAXDXEKWKWPXFXG
        KWLWSWCWFVFWDWGVFVGVHVI $.
        $( [12-May-2012] $) $( [22-Apr-2012] $)
    $}
  $}

  ${
    $d f a g h $.  $d a p q $.  $d f g h $.  $d L a f g $.  $d L p $.
    $d L x $.  $d P a f g h $.  $d R a f g h $.  $d R p q $.  $d R x y $.
    $d a b c d e j $.  $d A b c d e j $.  $d B b c d e j $.  $d C b c d e j $.
    $d f g h b c d e j $.  $d L b c d e j $.  $d P b c d e j $.
    $d R b c d e j $.  $d p q b c d e j $.  $d x y b c d e j $.
    axfelem18.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem18.2 $e |- P = |^| D $.
    axfelem18.3 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    axfelem18.4 $e |- M = U. C $.
    $( Lemma for axfe (future) .  At this point, we introduce a new surreal
       number ` M ` (for "middle").  The next several lemmas prove that either
       ` M ` or a closely related surreal has the required properties for the
       final theorem.  We begin by calculating the domain of ` M ` . $)
    axfelem18 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                         A. x e. L A. y e. R x <s y ) -> dom M = U. P ) $=
      ( vb vc ve vd vj csur wss wa wcel cslt wbr wral cdm ciun cuni dmeqi dmuni
      cv eqtri iunss cres wceq wrex axfelem12 abeq2i wi cin elssuni ssinss1 syl
      dmres dmeq syl5eqr syl5ibcom adantrd rexlimiv a1i rexlimivv sylbi mprgbir
      sseq1d wex csuc word wb con0 axfelem14 eloni ordsucuniel biimpd wo wne wn
      sylan reseq2 sylibr mpd rexbii rexnal bitri adantr sseldd breq2 ordtri2or
      impcom nodmord syl2anc orcanai sstr2 mpi jca ex wrel wfun funrel relssres
      nofun syl6 anassrs reximdva cvv vex resexg ax-mp anbi12d biantrur syl6bbr
      eqeq2 eqid eleq2d elin eleq2i 3bitr4ri cla4ev reximi eqeq1d exbii r19.41v
      rexcom4 rexcom onelon cint crab ssrab2 eqsstri onnmin con2i eleq2s adantl
      mpan eqeq12d necon3bid 2ralbidv axfelem11 elrab2 notbii sylib imnan df-ne
      con2bii weq simplll simprrl sltirr breq1 rcla42v ad2ant2l syl5ibrcom mtod
      ioran adantrr ordsuc sssucid simpllr simprrr anim12d anim12i eqtr2 expcom
      eqtr3 eqcoms syl5 exp3a com23 imp syl5bi ancld eqcom syl6bb sucid biantru
      jaodan sylbid anbi1d exbidv 2rexbidv rcla4ev eliun df-rex anbi1i 3bitr3ri
      mt3d jcad 3bitr3i 3bitr2i 3bitri syl6ibr ssrdv eqssd syl5eq ) LUFUGZHUFUG
      ZUHLCUIHDUIUHZUHZAURZBURZUJUKZBHULALULZUHZMUMZUAEUAURZUMZUNZGUOZUXTEUOZUM
      UYCMUYETUPUAEUQUSUXSUYCUYDUYCUYDUGZUXSUYFUYBUYDUGZUAEUAEUYBUYDUTUYAEUIZUB
      URZUCURZVAZUYAVBZUDURZUYJVAZUYAVBZUHZUCGVCZUDHVCZUBLVCZUYGUYSUAEEGHUCIJKL
      PUAUBUDSVDVEZUYQUYGUBUDLHUYQUYGVFUYILUIZUYMHUIZUHZUYPUYGUCGUYJGUIZUYLUYGU
      YOVUDUYJUYIUMZVGZUYDUGZUYLUYGVUDUYJUYDUGVUGUYJGVHUYJVUEUYDVIVJUYLVUFUYBUY
      DUYLVUFUYKUMUYBUYIUYJVKUYKUYAVLVMWAVNVOVPVQVRVSVTVQUXSUEUYDUYCUXSUEURZUYD
      UIZUYPVUHUYBUIZUHZUAWBZUDHVCZUBLVCZUCGVCZVUHUYCUIZUXSVUIVUHWCZGUIZUYIVUQV
      AZUYAVBZUYMVUQVAZUYAVBZUHZVUJUHZUAWBZUDHVCZUBLVCZUHVUOUXSVUIVURVVGUXSVUIV
      URUXSGWDZVUIVURWEUXSGWFUIZVVHABCDFGHLNOPQRWGZGWHVJVUHGWIVJZWJUXSVUIVURVVG
      VVKUXSVURVVGUXSVURUHZVUSVVAVBZVUHVUEUIZVUHUYMUMZUIZWKZUHZUDHVCZUBLVCZVVGV
      VLVVMUDHVCZUBLVCZVVTVVLVUSVVAWLZUDHULZUBLULZWMZVWBVVLVUQWFUIZVWFUXSVVIVUR
      VWGVVJGVUQUUAWNZVVLVWGVWEUHZWMZVWGVWFVFVVLVUQFUIZWMZVWJVURVWLUXSVWLVUQFUU
      BZGVWKVUQVWMUIZFWFUGVWKVWNWMFOURPURZVANURVWOVAWLNHULOLULZPWFUUCWFQVWPPWFU
      UDUUEFVUQUUFUUJUUGRUUHUUIVWKVWIUYIUYAVAZUYMUYAVAZWLZUDHULUBLULVWEUAVUQWFF
      UYAVUQVBZVWSVWCUBUDLHVWTVWQVWRVUSVVAVWTVWQVUSVWRVVAUYAVUQUYIWOUYAVUQUYMWO
      UUKUULUUMFHUDUBLNOPUAQUUNUUOUUPUUQVWGVWEUURWPWQVWBVWDWMZUBLVCVWFVWAVXAUBL
      VWAVWCWMZUDHVCVXAVVMVXBUDHVWCVVMVUSVVAUUSUUTWRVWCUDHWSWTWRVWDUBLWSWTWPVVL
      VWAVVSUBLVVLVUAUHZVVMVVRUDHVXCVUBUHVVMVVQVVLVUAVUBVVMVVQVFZUXSVURVUCVXDUX
      SVURVUCUHZUHZVVMVVQVXFVVMUHZVVQUBUDUVAZVXFVXHWMVVMVXFVXHUYIUYIUJUKZVXFUYI
      UFUIZVXIWMVXFLUFUYIUXSUXKVXEUXKUXLUXMUXRUVBXAUXSVURVUAVUBUVCXBZUYIUVDVJVX
      FVXIVXHUYIUYMUJUKZUXRVUCVXLUXNVURVUCUXRVXLUXQVXLUYIUXPUJUKABUYIUYMLHUXOUY
      IUXPUJUVEUXPUYMUYIUJXCUVFXEUVGUYIUYMUYIUJXCUVHUVIXAVVQWMVVNWMZVVPWMZUHZVX
      GVXHVVNVVPUVJVXFVVMVXOVXHVFVXFVXOVVMVXHVXFVXOVUSUYIVBZVVAUYMVBZUHZVVMVXHV
      FZVXFVXOVXJVUEVUQUGZUHZUYMUFUIZVVOVUQUGZUHZUHVXRVXFVXMVYAVXNVYDVXFVXMVYAV
      XFVXMUHZVXJVXTVXFVXJVXMVXKXAVYEVUEVUHUGZVXTVXFVVNVYFVXFVUHWDZVUEWDZVVNVYF
      WKVXFVWGVYGUXSVURVWGVUCVWHUVKVWGVUQWDVYGVUQWHVUHUVLWPVJZVXFVXJVYHVXKUYIXF
      VJVUHVUEXDXGXHVYFVUHVUQUGZVXTVUHUVMZVUEVUHVUQXIXJVJXKXLVXFVXNVYDVXFVXNUHZ
      VYBVYCVXFVYBVXNVXFHUFUYMUXSUXLVXEUXKUXLUXMUXRUVNXAUXSVURVUAVUBUVOXBZXAVYL
      VVOVUHUGZVYCVXFVVPVYNVXFVYGVVOWDZVVPVYNWKVYIVXFVYBVYOVYMUYMXFVJVUHVVOXDXG
      XHVYNVYJVYCVYKVVOVUHVUQXIXJVJXKXLUVPVYAVXPVYDVXQVXJUYIXMZVXTVXPVXJUYIXNVY
      PUYIXQUYIXOVJUYIVUQXPWNVYBUYMXMZVYCVXQVYBUYMXNVYQUYMXQUYMXOVJUYMVUQXPWNUV
      QXRVXQVXPVXSVXQVXPVVMVXHVXPVVMUHUYIVVAVBZVXQVXHVUSUYIVVAUVRVYRVXHVFUYMVVA
      VYRUYMVVAVBVXHUYIUYMVVAUVTUVSUWAUWBUWCXEXRUWDUWEUWFUXBXLXSXSUWGXTXTWQVVSV
      VFUBLVVRVVEUDHVVMVVNVVEVVPVVDVVMVVNUHUAVUSUYIYAUIVUSYAUIUBYBUYIVUQYAYCYDU
      YAVUSVBZVVCVVMVUJVVNVYSVVCVUSVUSVBZVVMUHVVMVYSVUTVYTVVBVVMUYAVUSVUSYHVYSV
      VBVVAVUSVBVVMUYAVUSVVAYHVVAVUSUWHUWIYEVYTVVMVUSYIYFYGVYSVUJVUHVUSUMZUIZVV
      NVYSUYBWUAVUHUYAVUSVLYJVUHVUQVUEVGZUIVUHVUQUIZVVNUHWUBVVNVUHVUQVUEYKWUAWU
      CVUHUYIVUQVKYLWUDVVNVUHUEYBUWJZYFYMYGYEYNVVDVVMVVPUHUAVVAUYMYAUIVVAYAUIUD
      YBUYMVUQYAYCYDUYAVVAVBZVVCVVMVUJVVPWUFVVCVVMVVAVVAVBZUHVVMWUFVUTVVMVVBWUG
      UYAVVAVUSYHUYAVVAVVAYHYEWUGVVMVVAYIUWKYGWUFVUJVUHVVAUMZUIZVVPWUFUYBWUHVUH
      UYAVVAVLYJVUHVUQVVOVGZUIWUDVVPUHWUIVVPVUHVUQVVOYKWUHWUJVUHUYMVUQVKYLWUDVV
      PWUEYFYMYGYEYNUWLYOYOVJXLUWMUXCVUNVVGUCVUQGUYJVUQVBZVULVVEUBUDLHWUKVUKVVD
      UAWUKUYPVVCVUJWUKUYLVUTUYOVVBWUKUYKVUSUYAUYJVUQUYIWOYPWUKUYNVVAUYAUYJVUQU
      YMWOYPYEUWNUWOUWPUWQXRVUPVUJUAEVCUYHVUJUHZUAWBZVUOUAVUHEUYBUWRVUJUAEUWSWU
      MUYSVUJUHZUAWBVUKUCGVCZUDHVCZUBLVCZUAWBZVUOWULWUNUAUYHUYSVUJUYTUWTYQWUQWU
      NUAWUQUYRVUJUHZUBLVCWUNWUPWUSUBLWUPUYQVUJUHZUDHVCWUSWUOWUTUDHUYPVUJUCGYRW
      RUYQVUJUDHYRWTWRUYRVUJUBLYRWTYQWUPUAWBZUBLVCVUMUCGVCZUBLVCWURVUOWVAWVBUBL
      VULUCGVCZUDHVCWUOUAWBZUDHVCWVBWVAWVCWVDUDHVUKUCUAGYSWRVULUDUCHGYTWUOUDUAH
      YSUXAWRWUPUBUALYSVUMUBUCLGYTUXDUXEUXFUXGUXHUXIUXJ $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    $( Lemma for axfe (future) . ` C ` is a subset of the surreals. $)
    axfelem19 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> C C_ No ) $=
      ( vc ve vb vd csur wss wa wcel cslt wbr wral cres wceq wrex cab axfelem12
      cv wi simplll sseld axfelem14 onelon ex syl noreson eleq1 biimpcd adantrd
      con0 sylan9 rexlimdv syld rexlimdvv abssdv syl5eqss ) LUEUFZHUEUFZUGLCUHH
      DUHUGZUGAUQBUQUIUJBHUKALUKZUGZEUAUQZUBUQZULZUCUQZUMZUDUQZWBULWDUMZUGZUBGU
      NZUDHUNUALUNZUCUOUEEGHUBIJKLPUCUAUDSUPVTWJUCUEVTWIWDUEUHZUAUDLHVTWALUHZWI
      WKURZWFHUHVTWLWAUEUHZWMVTLUEWAVPVQVRVSUSUTVTWNWMVTWNUGWHWKUBGVTWBGUHZWBVI
      UHZWNWHWKURZVTGVIUHZWOWPURABCDFGHLNOPQRVAWRWOWPGWBVBVCVDWNWPWQWNWPUGWCUEU
      HZWQWAWBVEWSWEWKWGWEWSWKWCWDUEVFVGVHVDVCVJVKVCVLVHVMVNVO $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

    ${
      $d A l m n o $.  $d a l m n o $.  $d B l m n o $.  $d b c d e l m n o $.
      $d C l m n o $.  $d f l m n o $.  $d g l m n o $.  $d h l n m o $.
      $d L l m n o $.  $d x y l m n o $.  $d P l m n o $.  $d R l m n o $.
      $( Lemma for axfe (future) .  Next, we prove that ` M ` is a function. $)
      axfelem20 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> Fun M ) $=
        ( vb vl vc ve vd vm vo vn csur wss wa wcel cslt wbr wral cuni axfelem19
        cv wfun sseld nofun syl6 cres wceq wrex axfelem12 abeq2i simplr anim12i
        wo wi simpll simpr jca32 con0 axfelem14 onelon ex word eloni ordtri2or2
        syl syl2an simplll jca adantr simprll simprrl simprrr adantl reseq1 cin
        ad2antlr resres eqeq12i sseqin2 reseq2 sylbi syl5bb syl5ibcom axfelem17
        wb eqeq12d imp syl112anc simpld ssres2 sseq1 syl5ibrcom simprlr orim12d
        mpd exp32 com34 mpdi syl2and imp3a syl5 anassrs expr eqeq1 eqcom syl6bb
        sseq2 orbi12d imbi12d biimpcd rexlimdva rexlimdvv ralrimiv ralbidv jcad
        syl5bi fununi funeqi sylibr ) LUIUJHUIUJUKZLCULHDULUKZUKZAURBURUMUNBHUO
        ALUOZUKZEUPZUSZMUSUUAUAURZUSZUUDUBURZUJZUUFUUDUJZVJZUBEUOZUKZUAEUOUUCUU
        AUUKUAEUUAUUDEULZUUEUUJUUAUULUUDUIULUUEUUAEUIUUDABCDEFGHIJKLMNOPQRSTUQU
        TUUDVAVBUULUCURZUDURZVCZUUDVDZUEURZUUNVCZUUDVDZUKZUDGVEZUEHVEUCLVEZUUAU
        UJUVBUAEEGHUDIJKLPUAUCUESVFVGUUAUVAUUJUCUELHUUAUUMLULUUQHULUKZUVAUUJVKU
        UAUVCUKUUTUUJUDGUUAUVCUUNGULZUUTUUJVKZUUAUVCUVDUKZUKUUOUURVDZUUOUUFUJZU
        UFUUOUJZVJZUBEUOZVKZUVEUUAUVFUVGUVKUUAUVFUVGUKZUKZUVJUBEUUFEULUFURZUGUR
        ZVCZUUFVDZUHURZUVPVCZUUFVDZUKZUGGVEZUHHVEUFLVEZUVNUVJUWDUBEEGHUGIJKLPUB
        UFUHSVFVGUVNUWCUVJUFUHLHUVNUVOLULUVSHULUKZUWCUVJVKUVNUWEUKUWBUVJUGGUVNU
        WEUVPGULZUWBUVJVKZUVNUWEUWFUKZUKUVQUVTVDZUUOUVQUJZUVQUUOUJZVJZVKZUWGUVN
        UWHUWIUWLUUAUVMUWHUWIUKZUWLUUAUVMUWNUKZUWLUWOUVDUWFUKZUVCUWEUKZUVGUWIUK
        ZUKZUKUUAUWLUWOUWPUWQUWRUVMUVDUWNUWFUVCUVDUVGVHUWEUWFUWIVHVIUVMUVCUWNUW
        EUVCUVDUVGVLUWEUWFUWIVLVIUVMUVGUWNUWIUVFUVGVMUWHUWIVMVIVNUUAUWPUWSUWLUU
        AUVDUUNVOULZUWFUVPVOULZUWSUWLVKZUUAGVOULZUVDUWTVKABCDFGHLNOPQRVPZUXCUVD
        UWTGUUNVQVRWBUUAUXCUWFUXAVKUXDUXCUWFUXAGUVPVQVRWBUUAUWTUXAUKZUUNUVPUJZU
        VPUUNUJZVJZUXBUWTUUNVSUVPVSUXHUXAUUNVTUVPVTUUNUVPWAWCUUAUXEUWSUXHUWLUUA
        UXEUWSUXHUWLVKUUAUXEUWSUKZUKZUXFUWJUXGUWKUXJUXFUWJUXJUXFUKZUUOUVOUUNVCZ
        VDZUWJUXKUXMUURUVSUUNVCZVDZUXKYQYTUKZUWTUWQUKZUVGUXLUXNVDZUXMUXOUKUXJUX
        PUXFUXJYQYTYQYRYTUXIWDYSYTUXIVHWEZWFUXJUXQUXFUXJUWTUWQUUAUWTUXAUWSWGUUA
        UXEUWQUWRWHZWEWFUXIUVGUUAUXFUXEUWQUVGUWIWHZWMUXJUXFUXRUXJUWIUXFUXRVKUXI
        UWIUUAUXEUWQUVGUWIWIZWJUWIUVQUUNVCZUVTUUNVCZVDZUXFUXRUVQUVTUUNWKUYEUVOU
        VPUUNWLZVCZUVSUYFVCZVDZUXFUXRUYCUYGUYDUYHUVOUVPUUNWNUVSUVPUUNWNWOUXFUYF
        UUNVDZUYIUXRXBUUNUVPWPUYJUYGUXLUYHUXNUYFUUNUVOWQUYFUUNUVSWQXCWRWSWTWBXD
        ABLHUUMUUQUVOUVSUUNXAXEXFUXFUXMUWJVKUXJUXFUWJUXMUXLUVQUJUUNUVPUVOXGUUOU
        XLUVQXHXIWJXLVRUXJUXGUWKUXJUXGUKZUUMUVPVCZUVQVDZUWKUYKUYMUUQUVPVCZUVTVD
        ZUYKUXPUXAUWQUKZUYLUYNVDZUWIUYMUYOUKUXJUXPUXGUXSWFUXJUYPUXGUXJUXAUWQUUA
        UWTUXAUWSXJUXTWEWFUXJUXGUYQUXJUVGUXGUYQVKUXIUVGUUAUYAWJUVGUUOUVPVCZUURU
        VPVCZVDZUXGUYQUUOUURUVPWKUYTUUMUUNUVPWLZVCZUUQVUAVCZVDZUXGUYQUYRVUBUYSV
        UCUUMUUNUVPWNUUQUUNUVPWNWOUXGVUAUVPVDZVUDUYQXBUVPUUNWPVUEVUBUYLVUCUYNVU
        AUVPUUMWQVUAUVPUUQWQXCWRWSWTWBXDUXIUWIUUAUXGUYBWMABLHUUMUUQUVOUVSUVPXAX
        EXFUXGUYMUWKVKUXJUXGUYLUUOUJUYMUWKUVPUUNUUMXGUYLUVQUUOXHWTWJXLVRXKXMXNX
        OXPXQXRXDXSXTUWMUVRUWAUVJUVRUWMUWAUVJVKUVRUWIUWAUWLUVJUVRUWIUUFUVTVDUWA
        UVQUUFUVTYAUUFUVTYBYCUVRUWJUVHUWKUVIUVQUUFUUOYDUVQUUFUUOXHYEYFYGXQWBXSY
        HVRYIYMYJXTUVLUUPUUSUUJUUPUVLUUSUUJVKUUPUVGUUSUVKUUJUUPUVGUUDUURVDUUSUU
        OUUDUURYAUUDUURYBYCUUPUVJUUIUBEUUPUVHUUGUVIUUHUUOUUDUUFXHUUOUUDUUFYDYEY
        KYFYGXQWBXSYHVRYIYMYLYJEUAUBYNWBMUUBTYOYP $.
        $( [12-May-2012] $) $( [22-Apr-2012] $)
    $}

    $( Lemma for axfe (future) .  Now, using the previous three lemmas, we show
       that ` M ` is a surreal. $)
    axfelem21 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> M e. No ) $=
      ( vb csur wss wa wcel cv cslt wbr wral wfun cdm crn c1o c2o cpr axfelem20
      con0 w3a cuni axfelem18 axfelem14 onuni syl eqeltrd rneqi rnuni axfelem19
      ciun eqtri sseld norn syl6 ralrimiv iunss sylibr syl5eqss 3jca elno2 ) LU
      BUCHUBUCUDLCUEHDUEUDUDAUFBUFUGUHBHUIALUIUDZMUJZMUKZUQUEZMULZUMUNUOZUCZURM
      UBUEVSVTWBWEABCDEFGHIJKLMNOPQRSTUPVSWAGUSZUQABCDEFGHIJKLMNOPQRSTUTVSGUQUE
      WFUQUEABCDFGHLNOPQRVAGVBVCVDVSWCUAEUAUFZULZVHZWDWCEUSZULWIMWJTVEUAEVFVIVS
      WHWDUCZUAEUIWIWDUCVSWKUAEVSWGEUEWGUBUEWKVSEUBWGABCDEFGHIJKLMNOPQRSTVGVJWG
      VKVLVMUAEWHWDVNVOVPVQMVRVO $.
      $( [12-May-2012] $) $( [22-Apr-2012] $)

  $}

  ${
    $d a f g h $.  $d a l p q $.  $d r a $.  $d f g h $.  $d K l r $.
    $d L a f g $.  $d L l p $.  $d l q r $.  $d L r $.  $d L x $.  $d M l r $.
    $d P a f g h $.  $d q p $.  $d P l r $.  $d r q $.  $d R a f g h $.
    $d R l p q $.  $d R r $.  $d R x y $.  $d a f g h b c d e t $.
    $d l r b c d e t $.  $d K b c d e t $.  $d L b c d e t $.
    $d P b c d e t $.  $d R b c d e t $.  $d p q b c d e t $.
    axfelem22.1 $e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } $.
    axfelem22.2 $e |- P = |^| D $.
    axfelem22.3 $e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } $.
    axfelem22.4 $e |- M = U. C $.
    $( Lemma for axfe (future) .  Given an element ` K ` of ` P ` , there are
       elements ` l ` and ` r ` of ` L ` and ` R ` whose restrictions to ` K `
       are a prefix of ` M ` . $)
    axfelem22 $p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) -> ( K e. P ->
                       ( E. l e. L ( l |` K ) C_ M /\
                         E. r e. R ( r |` K ) C_ M ) ) ) $=
      ( vd vt vb vc ve csur wss wa wcel cv cslt wbr wral con0 cres wi axfelem14
      wrex wceq wne wn cint crab ssrab2 eqsstri onnmin mpan con2i eleq2s adantl
      onelon reseq2 eqeq12d necon3bid axfelem11 elrab2 notbii imnan bitr4i sylc
      biimpi df-ne con2bii rexbii rexnal bitri sylibr eqeq1d anbi12d eqcom eqid
      2ralbidv biantrur biantru anbi12i anidm bitr3i syl6bb rcla4ev reximdv mpd
      ex w3a weq reseq1 anbi1d rexbidv anbi2d rcla42ev r19.40 reximi syl resexg
      cuni cvv vex ax-mp eqeq2 axfelem12 elab2 elssuni sylbir syl6sseqr anim12i
      2rexbidv 3syl 3expia reximdva reximia reeanv sylib ) MUIUJHUIUJUKMCULHDUL
      UKUKAUMBUMUNUOBHUPAMUPUKGUQULZLGULZSUMZLURZNUJZSMVAOUMZLURZNUJZOHVAUKZUSA
      BCDFGHMPQRTUAUTYOYPUUCYOYPUKZYQUDUMZURZYRVBZYTUUEURZYRVBZUKZUUFUUAVBZUUHU
      UAVBZUKZUKZUDGVAZOHVAZSMVAZUUCUUDYRUUAVBZOHVAZSMVAZUUQUUDYRUUAVCZOHUPZSMU
      PZVDZUUTUUDLFULZVDZLUQULZUVDYPUVFYOUVFLFVEZGUVELUVHULZFUQUJUVEUVIVDFQUMRU
      MZURPUMUVJURVCPHUPQMUPZRUQVFUQTUVKRUQVGVHFLVIVJVKUAVLVMGLVNUVFUVGUVDUSZUV
      FUVGUVCUKZVDUVLUVEUVMYQUEUMZURZYTUVNURZVCZOHUPSMUPUVCUELUQFUVNLVBZUVQUVAS
      OMHUVRUVOUVPYRUUAUVRUVOYRUVPUUAUVNLYQVOUVNLYTVOVPVQWOFHOSMPQRUETVRVSVTUVG
      UVCWAWBWDWCUUTUVBVDZSMVAUVDUUSUVSSMUUSUVAVDZOHVAUVSUURUVTOHUVAUURYRUUAWEW
      FWGUVAOHWHWIWGUVBSMWHWIWJYPUUTUUQUSYOYPUUSUUPSMYPUURUUOOHYPUURUUOUUNUURUD
      LGUUELVBZUUNYRYRVBZUUAYRVBZUKZUURUUAUUAVBZUKZUKZUURUWAUUJUWDUUMUWFUWAUUGU
      WBUUIUWCUWAUUFYRYRUUELYQVOZWKUWAUUHUUAYRUUELYTVOZWKWLUWAUUKUURUULUWEUWAUU
      FYRUUAUWHWKUWAUUHUUAUUAUWIWKWLWLUWGUURUURUKUURUURUWDUURUWFUURUWCUWDYRUUAW
      MUWBUWCYRWNWPWIUWEUURUUAWNWQWRUURWSWTXAXBXEXCXCVMXDUUQYSUUBUKZOHVAZSMVAUU
      CUUPUWKSMYQMULZUUOUWJOHUWLYTHULZUUOUWJUWLUWMUUOXFUFUMZUUEURZYRVBZUGUMZUUE
      URZYRVBZUKZUWOUUAVBZUWRUUAVBZUKZUKZUDGVAZUGHVAZUFMVAZUWTUDGVAZUGHVAZUFMVA
      ZUXCUDGVAZUGHVAZUFMVAZUKZUWJUXEUUOUUGUWSUKZUUKUXBUKZUKZUDGVAUFUGYQYTMHUFS
      XGZUXDUXQUDGUXRUWTUXOUXCUXPUXRUWPUUGUWSUXRUWOUUFYRUWNYQUUEXHZWKXIUXRUXAUU
      KUXBUXRUWOUUFUUAUXSWKXIWLXJUGOXGZUXQUUNUDGUXTUXOUUJUXPUUMUXTUWSUUIUUGUXTU
      WRUUHYRUWQYTUUEXHZWKXKUXTUXBUULUUKUXTUWRUUHUUAUYAWKXKWLXJXLUXGUXIUXLUKZUF
      MVAUXNUXFUYBUFMUXFUXHUXKUKZUGHVAUYBUXEUYCUGHUWTUXCUDGXMXNUXHUXKUGHXMXOXNU
      XIUXLUFMXMXOUXJYSUXMUUBUXJYREXQZNUXJYREULYRUYDUJUWOUHUMZVBZUWRUYEVBZUKZUD
      GVAZUGHVAUFMVAZUXJUHYREYQXRULYRXRULSXSYQLXRXPXTUYEYRVBZUYIUXHUFUGMHUYKUYH
      UWTUDGUYKUYFUWPUYGUWSUYEYRUWOYAUYEYRUWRYAWLXJYHEGHUDIJKMRUHUFUGUBYBZYCYRE
      YDYEUCYFUXMUUAUYDNUXMUUAEULUUAUYDUJUYJUXMUHUUAEYTXRULUUAXRULOXSYTLXRXPXTU
      YEUUAVBZUYIUXKUFUGMHUYMUYHUXCUDGUYMUYFUXAUYGUXBUYEUUAUWOYAUYEUUAUWRYAWLXJ
      YHUYLYCUUAEYDYEUCYFYGYIYJYKYLYSUUBSOMHYMYNXOXEXO $.
      $( [12-May-2012] $) $( [23-Apr-2012] $)
  $}

  $(
  @{
     axfelem23.1 @e |- D = { a e. On |
                        A. p e. L A. q e. R ( p |` a ) =/= ( q |` a ) } @.
     axfelem23.2 @e |- P = |^| D @.
     axfelem23.3 @e |- C = { f | E. g e. L E. h e. R E. a e. P
                       ( ( g |` a ) = f /\ ( h |` a ) = f ) } @.
     axfelem23.4 @e |- M = U. C @.

     @( Lemma for axfe (future) . There are elements of ` L ` and ` R `
        whose restrictions to ` U. P ` are ` M ` . @)
     axfelem23 @p |- ( ( ( ( L C_ No /\ R C_ No ) /\ ( L e. A /\ R e. B ) ) /\
                       A. x e. L A. y e. R x <s y ) ->
                       ( E. l e. L ( l |` U. P ) = M /\
                         E. r e. R ( r |` U. P ) = M ) ) @= ? @.
  @}
  $)

