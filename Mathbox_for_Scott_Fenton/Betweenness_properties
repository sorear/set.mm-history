$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Betweenness properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d N x $.
    $( Betweeness always holds for the second endpoint.  Theorem 3.1 of
       [Schwabhauser], p. 30.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
    btwntriv2 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
      B Btwn <. A , B >. ) $=
      ( vx cn wcel cee cfv w3a cv cop cbtwn wbr ccgr wa wrex simp1 simp2 simp3
      wi axsegcon syl122anc simpl1 simpl3 simpr axcgrid syl13anc breq2d biimprd
      wceq opeq2 syl6 imp3a ancomsd rexlimdva mpd ) CEFZACGHZFZBURFZIZBADJZKZLM
      ZBVBKBBKNMZOZDURPZBABKZLMZVAUQUSUTUTUTVGUQUSUTQUQUSUTRUQUSUTSZVJVJDABBBCU
      AUBVAVFVIDURVAVBURFZOZVEVDVIVLVEVDVIVLVEBVBUJZVDVITVLUQUTVKUTVEVMTUQUSUTV
      KUCUQUSUTVKUDZVAVKUEVNBVBBCUFUGVMVIVDVMVHVCBLBVBAUKUHUIULUMUNUOUP $.
      $( [12-Jun-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.
    $( Betweeness commutes.  Implication version.  Theorem 3.2 of
       [Schwabhauser], p. 30.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
    btwncomim $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Btwn <. B , C >. -> A Btwn <. C , B >. ) ) $=
      ( vx cn wcel cee cfv w3a wa cop cbtwn wbr cv btwntriv2 3adant3r2 wi simpl
      wrex simpr2 simpr1 simpr3 axpasch syl132anc mpan2d simpr simplr1 axbtwnid
      wceq simpll syl111anc breq1 biimpd syl6 imp3a rexlimdva syld ) DFGZADHIZG
      ZBUTGZCUTGZJZKZABCLMNZEOZAALMNZVGCBLZMNZKZEUTTZAVIMNZVEVFCACLMNZVLUSVAVCV
      NVBACDPQVEUSVBVAVCVAVCVFVNKVLRUSVDSUSVAVBVCUAUSVAVBVCUBZUSVAVBVCUCZVOVPEB
      ACACDUDUEUFVEVKVMEUTVEVGUTGZKZVHVJVMVRVHVGAUJZVJVMRVRUSVQVAVHVSRUSVDVQUKV
      EVQUGVAVBVCUSVQUHVGADUIULVSVJVMVGAVIMUMUNUOUPUQUR $.
      $( [12-Jun-2013] $)
  $}

  $( Betweeness commutes.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  btwncom $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Btwn <. B , C >. <-> A Btwn <. C , B >. ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn wbr btwncomim wi 3ancomb sylan2b impbid
    ) DEFZADGHZFZBTFZCTFZIZJABCKLMZACBKLMZABCDNUDSUAUCUBIUFUEOUAUBUCPACBDNQR $.
    $( [12-Jun-2013] $)

  ${
    btwncomand.1 $e |- ( ph -> N e. NN ) $.
    btwncomand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    btwncomand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    btwncomand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    btwncomand.5 $e |- ( ( ph /\ ps ) -> A Btwn <. B , C >. ) $.
    $( Deduction form of ~ btwncom .  (Contributed by Scott Fenton,
       14-Oct-2013.) $)
    btwncomand $p |- ( ( ph /\ ps ) -> A Btwn <. C , B >. ) $=
      ( wa cop cbtwn wbr wb cn wcel cee cfv btwncom syl13anc adantr mpbid ) ABL
      CDEMNOZCEDMNOZKAUEUFPZBAFQRCFSTZRDUHREUHRUGGHIJCDEFUAUBUCUD $.
      $( [14-Oct-2013] $) $( [14-Oct-2013] $)
  $}

  $( Betweeness always holds for the first endpoint.  Theorem 3.3 of
     [Schwabhauser], p. 30.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  btwntriv1 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
      A Btwn <. A , B >. ) $=
    ( cn wcel cee cfv w3a cop cbtwn wbr btwntriv2 3com23 wb simp1 simp2 btwncom
    simp3 syl13anc mpbird ) CDEZACFGZEZBUBEZHZAABIJKZABAIJKZUAUDUCUGBACLMUEUAUC
    UCUDUFUGNUAUCUDOUAUCUDPZUHUAUCUDRAABCQST $.
    $( [12-Jun-2013] $)

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.
    $( If you can swap the first two arguments of a betweenness statement, then
       those arguments are identical.  Theorem 3.4 of [Schwabhauser], p. 30.
       (Contributed by Scott Fenton, 12-Jun-2013.) $)
    btwnswapid $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       ( ( A Btwn <. B , C >. /\ B Btwn <. A , C >. ) ->
         A = B ) ) $=
      ( vx cn wcel cee cfv w3a wa cop cbtwn wbr cv wrex wceq axbtwnid syl111anc
      wi simpl simpr2 simpr1 simpr3 axpasch simpr simplr1 simplr2 anim12d eqtr2
      syl132anc simpll syl6 rexlimdva syld ) DFGZADHIZGZBUQGZCUQGZJZKZABCLMNBAC
      LMNKZEOZAALMNZVDBBLMNZKZEUQPZABQZVBUPUSURUTURUSVCVHTUPVAUAUPURUSUTUBZUPUR
      USUTUCZUPURUSUTUDVKVJEBACABDUEUKVBVGVIEUQVBVDUQGZKZVGVDAQZVDBQZKVIVMVEVNV
      FVOVMUPVLURVEVNTUPVAVLULZVBVLUFZURUSUTUPVLUGVDADRSVMUPVLUSVFVOTVPVQURUSUT
      UPVLUHVDBDRSUIVDABUJUMUNUO $.
      $( [12-Jun-2013] $)
  $}

  $( If you can swap arguments one and three of a betweenness statement, then
     those arguments are identical.  (Contributed by Scott Fenton,
     7-Oct-2013.) $)
  btwnswapid2 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       ( ( A Btwn <. B , C >. /\ C Btwn <. B , A >. ) ->
         A = C ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn wbr btwncom wb 3anrev sylan2b anbi12d wi
    wceq 3ancomb btwnswapid sylbid ) DEFZADGHZFZBUEFZCUEFZIZJZABCKLMZCBAKLMZJAC
    BKLMZCABKLMZJZACTZUJUKUMULUNABCDNUIUDUHUGUFIULUNOUFUGUHPCBADNQRUIUDUFUHUGIU
    OUPSUFUGUHUAACBDUBQUC $.
    $( [14-Oct-2013] $) $( [7-Oct-2013] $)

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Inner transitivity law for betweenness.  Left hand side of Theorem 3.5
       of [Schwabhauser], p. 30.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnintr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , D >. /\ C Btwn <. B , D >. ) ->
         B Btwn <. A , C >. ) ) $=
      ( vx cn wcel cee cfv wa w3a cop cbtwn wbr cv wrex wi simp1 syld syl132anc
      simp2l simp2r simp3r simp3l axpasch wceq simpr simpl2r axbtwnid syl111anc
      simpl1 breq1 biimpa wb simpl3l simpl2l btwncom syl13anc biimpd syl5 exp3a
      imp3a rexlimdva ) EGHZAEIJZHZBVFHZKZCVFHZDVFHZKZLZBADMNOCBDMNOKZFPZBBMNOZ
      VOCAMZNOZKZFVFQZBACMNOZVMVEVGVHVKVHVJVNVTRVEVIVLSVEVGVHVLUBVEVGVHVLUCZVEV
      IVJVKUDWBVEVIVJVKUEFABDBCEUFUAVMVSWAFVFVMVOVFHZKZVPVRWAWDVPVOBUGZVRWARWDV
      EWCVHVPWERVEVIVLWCULZVMWCUHVGVHVEVLWCUIZVOBEUJUKWDWEVRWAWEVRKBVQNOZWDWAWE
      VRWHVOBVQNUMUNWDWHWAWDVEVHVJVGWHWAUOWFWGVJVKVEVIWCUPVGVHVEVLWCUQBCAEURUSU
      TVAVBTVCVDT $.
      $( [12-Jun-2013] $)

    $( Exchange the first endpoint in betweenness.  Left hand side of Theorem
       3.6 of [Schwabhauser], p. 30.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnexch3 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , C >. /\ C Btwn <. A , D >. ) ->
         C Btwn <. B , D >. ) ) $=
      ( vx cn wcel cee cfv wa w3a cop cbtwn wbr cv wb btwncom syl13anc wi simp1
      wrex simp3l simp2l simp3r simp2r anbi12d axpasch syl132anc sylbid ancomsd
      wceq simpl1 simpr simpl3l axbtwnid syl111anc breq1 biimpd imp3a rexlimdva
      syl6 syld ) EGHZAEIJZHZBVEHZKZCVEHZDVEHZKZLZBACMNOZCADMNOZKFPZCCMNOZVOBDM
      ZNOZKZFVEUBZCVQNOZVLVNVMVTVLVNVMKCDAMNOZBCAMNOZKZVTVLVNWBVMWCVLVDVIVFVJVN
      WBQVDVHVKUAZVDVHVIVJUCZVDVFVGVKUDZVDVHVIVJUEZCADERSVLVDVGVFVIVMWCQWEVDVFV
      GVKUFZWGWFBACERSUGVLVDVJVIVFVIVGWDVTTWEWHWFWGWFWIFDCACBEUHUIUJUKVLVSWAFVE
      VLVOVEHZKZVPVRWAWKVPVOCULZVRWATWKVDWJVIVPWLTVDVHVKWJUMVLWJUNVIVJVDVHWJUOV
      OCEUPUQWLVRWAVOCVQNURUSVBUTVAVC $.
      $( [12-Jun-2013] $)

    ${
      btwnexch3and.1 $e |- ( ph -> N e. NN ) $.
      btwnexch3and.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
      btwnexch3and.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
      btwnexch3and.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
      btwnexch3and.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
      btwnexch3and.6 $e |- ( ( ph /\ ps ) -> B Btwn <. A , C >. ) $.
      btwnexch3and.7 $e |- ( ( ph /\ ps ) -> C Btwn <. A , D >. ) $.
      $( Deduction form of ~ btwnexch3 .  (Contributed by Scott Fenton,
         13-Oct-2013.) $)
      btwnexch3and $p |- ( ( ph /\ ps ) -> C Btwn <. B , D >. ) $=
        ( wa cop cbtwn wbr wi wcel cn cee cfv btwnexch3 syl122anc adantr mp2and
        ) ABODCEPQRZECFPQRZEDFPQRZMNAUHUIOUJSZBAGUATCGUBUCZTDULTEULTFULTUKHIJKL
        CDEFGUDUEUFUG $.
        $( [14-Oct-2013] $) $( [13-Oct-2013] $)
    $}

    $( Outer transitivity law for betweenness.  Left hand side of Theorem 3.1
       of [Schwabhauser], p. 30.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnouttr2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B =/= C /\
           B Btwn <. A , C >. /\ C Btwn <. B , D >. ) ->
         C Btwn <. A , D >. ) ) $=
      ( vx cn wcel cee wa w3a cop cbtwn wbr ccgr syl122anc adantr wi jca mpd cv
      cfv wne simp1 simp2l simp3l simp3r axsegcon simprrl simprl1 simpl2 simprl
      wrex wceq adantl simpl1 simpl2l simpl2r simpl3l btwnexch3 simprrr simprl3
      simpr simpl3r cgrrflx syl111anc 3jca segconeq syl133anc opeq2d expr an32s
      breqtrd rexlimdva ex ) EGHZAEIUBZHZBVQHZJZCVQHZDVQHZJZKZBCUCZBACLMNZCBDLM
      NZKZCADLZMNZWDWHJZCAFUAZLZMNZCWLLCDLZONZJZFVQUMZWJWDWRWHWDVPVRWAWAWBWRVPV
      TWCUDVPVRVSWCUEVPVTWAWBUFZWSVPVTWAWBUGFACCDEUHPQWKWQWJFVQWDWLVQHZWHWQWJRW
      DWTJZWHWQWJXAWHWQJZJZCWMWIMXAWHWNWPUIXCWLDAXCWECBWLLMNZWPJZWGWOWOONZJZKZW
      LDUNZXCWEXEXGWEWFWGWQXAUJXCXDWPXCWFWNJZXDXBXJXAXBWFWNWEWFWGWQUKWHWNWPULSU
      OXAXJXDRZXBXAVPVRVSWAWTXKVPVTWCWTUPZVRVSVPWCWTUQVRVSVPWCWTURZWAWBVPVTWTUS
      ZWDWTVCZABCWLEUTPQTXAWHWNWPVASXCWGXFWEWFWGWQXAVBXAXFXBXAVPWAWBXFXLXNWAWBV
      PVTWTVDZCDEVEVFQSVGXAXHXIRZXBXAVPWAWAWBVSWTWBXQXLXNXNXPXMXOXPCCDBEWLDVHVI
      QTVJVMVKVLVNTVO $.
      $( [12-Jun-2013] $)
  $}

  $( Exchange the outer point of two betweenness statements.  Right hand side
     of Theorem 3.5 of [Schwabhauser], p. 30.  (Contributed by Scott Fenton,
     14-Jun-2013.) $)
  btwnexch2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
   ( ( B Btwn <. A , D >. /\ C Btwn <. B , D >. ) ->
     C Btwn <. A , D >. ) ) $=
    ( cn wcel cee cfv wa w3a cop cbtwn wbr wi wceq breq1 biimpd adantr mpd 3jca
    adantrd a1i wne simprl simprr btwnintr simprrr btwnouttr2 exp32 pm2.61dne )
    EFGAEHIZGBULGJCULGDULGJKZBADLZMNZCBDLMNZJZCUNMNZOZBCBCPZUSOUMUTUOURUPUTUOUR
    BCUNMQRUBUCUMBCUDZUQURUMVAUQJZJZVABACLMNZUPKZURVCVAVDUPUMVAUQUEVCUQVDUMVAUQ
    UFUMUQVDOVBABCDEUGSTUMVAUOUPUHUAUMVEUROVBABCDEUISTUJUK $.
    $( [14-Jun-2013] $)

  $( Outer transitivity law for betweenness.  Right hand side of Theorem 3.7 of
     [Schwabhauser], p. 30.  (Contributed by Scott Fenton, 14-Jun-2013.) $)
  btwnouttr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
   ( ( B =/= C /\
       B Btwn <. A , C >. /\ C Btwn <. B , D >. ) ->
     B Btwn <. A , D >. ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wb necom a1i btwncom syl13anc wi
    simp1 simp2r simp2l simp3l simp3r 3anbi123d 3ancomb syl6bb biimpa syl122anc
    btwnouttr2 adantr mpd simpl1 simpl2r simpl2l simpl3r mpbird ex ) EFGZAEHIZG
    ZBVBGZJZCVBGZDVBGZJZKZBCLZBACMNOZCBDMNOZKZBADMNOZVIVMJZVNBDAMNOZVOCBLZCDBMN
    OZBCAMNOZKZVPVIVMVTVIVMVQVSVRKVTVIVJVQVKVSVLVRVJVQPVIBCQRVIVAVDVCVFVKVSPVAV
    EVHUBZVAVCVDVHUCZVAVCVDVHUDZVAVEVFVGUEZBACESTVIVAVFVDVGVLVRPWAWDWBVAVEVFVGU
    FZCBDESTUGVQVSVRUHUIUJVIVTVPUAZVMVIVAVGVFVDVCWFWAWEWDWBWCDCBAEULUKUMUNVOVAV
    DVCVGVNVPPVAVEVHVMUOVCVDVAVHVMUPVCVDVAVHVMUQVFVGVAVEVMURBADESTUSUT $.
    $( [14-Jun-2013] $)

  $( Outer transitivity law for betweenness.  Right hand side of Theorem 3.6 of
     [Schwabhauser], p. 30.  (Contributed by Scott Fenton, 24-Sep-2013.) $)
  btwnexch $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
  ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
  ( ( B Btwn <. A , C >. /\ C Btwn <. A , D >. ) -> B Btwn <. A , D >. ) ) $=
    ( cn wcel cee cfv wa w3a cop cbtwn wbr simp1 simp2r simp2l btwncom syl13anc
    wb simp3l simp3r anbi12d ancom syl6bb wi btwnexch2 syl122anc sylbid sylibd
    ) EFGZAEHIZGZBULGZJZCULGZDULGZJZKZBACLMNZCADLZMNZJZBDALZMNZBVAMNZUSVCCVDMNZ
    BCALMNZJZVEUSVCVHVGJVIUSUTVHVBVGUSUKUNUMUPUTVHTUKUOUROZUKUMUNURPZUKUMUNURQZ
    UKUOUPUQUAZBACERSUSUKUPUMUQVBVGTVJVMVLUKUOUPUQUBZCADERSUCVHVGUDUEUSUKUQUPUN
    UMVIVEUFVJVNVMVKVLDCBAEUGUHUIUSUKUNUQUMVEVFTVJVKVNVLBDAERSUJ $.
    $( [14-Oct-2013] $) $( [24-Sep-2013] $)

  ${
    btwnexchand.1 $e |- ( ph -> N e. NN ) $.
    btwnexchand.2 $e |- ( ph -> A e. ( EE ` N ) ) $.
    btwnexchand.3 $e |- ( ph -> B e. ( EE ` N ) ) $.
    btwnexchand.4 $e |- ( ph -> C e. ( EE ` N ) ) $.
    btwnexchand.5 $e |- ( ph -> D e. ( EE ` N ) ) $.
    btwnexchand.6 $e |- ( ( ph /\ ps ) -> B Btwn <. A , C >. ) $.
    btwnexchand.7 $e |- ( ( ph /\ ps ) -> C Btwn <. A , D >. ) $.
    $( Deduction form of ~ btwnexch .  (Contributed by Scott Fenton,
       13-Oct-2013.) $)
    btwnexchand $p |- ( ( ph /\ ps ) -> B Btwn <. A , D >. ) $=
      ( wa cop cbtwn wbr wi wcel cn cee cfv btwnexch syl122anc adantr mp2and )
      ABODCEPQRZECFPZQRZDUIQRZMNAUHUJOUKSZBAGUATCGUBUCZTDUMTEUMTFUMTULHIJKLCDEF
      GUDUEUFUG $.
      $( [14-Oct-2013] $) $( [13-Oct-2013] $)
  $}


  ${
    $d A c u v $.  $d B c u v $.  $d N c u v $.
    $( There is always a ` c ` distinct from ` B ` such that ` B ` lies between
       ` A ` and ` c ` .  Theorem 3.14 of [Schwabhauser], p. 32.  (Contributed
       by Scott Fenton, 24-Sep-2013) $)
    btwndiff $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
       E. c e. ( EE ` N ) ( B Btwn <. A , c >. /\ B =/= c ) ) $=
      ( vu vv cn wcel cee cfv w3a cv wne wrex cop wbr wa syl122anc wi mpd cbtwn
      axlowdim1 3ad2ant1 ccgr simp11 simp12 simp13 simp2l axsegcon wceq simpl11
      simp2r weq wb simpl13 simpr simpl2l simpl2r cgrdegen bi1 necon3d 3ad2ant3
      com12 adantr syld anim2d reximdva 3exp rexlimdvv ) CGHZACIJZHZBVKHZKZELZF
      LZMZFVKNEVKNZBADLZOUAPZBVSMZQZDVKNZVJVLVRVMEFCUBUCVNVQWCEFVKVKVNVOVKHZVPV
      KHZQZVQWCVNWFVQKZVTBVSOVOVPOUDPZQZDVKNZWCWGVJVLVMWDWEWJVJVLVMWFVQUEVJVLVM
      WFVQUFVJVLVMWFVQUGVNWDWEVQUHVNWDWEVQULDABVOVPCUIRWGWIWBDVKWGVSVKHZQZWHWAV
      TWLWHBVSUJZEFUMZUNZWAWLVJVMWKWDWEWHWOSVJVLVMWFVQWKUKVJVLVMWFVQWKUOWGWKUPW
      DWEVNVQWKUQWDWEVNVQWKURBVSVOVPCUSRWGWOWASZWKVQVNWPWFWOVQWAWOBVSVOVPWMWNUT
      VAVCVBVDVEVFVGTVHVIT $.
      $( [14-Oct-2013] $) $( [24-Sep-2013] $)
  $}

  ${
    $d A q r $.  $d B q r $.  $d C q r $.  $d D q r $.  $d E q r $.
    $d N q r $.  $d P q r $.
    $( A line segment between two sides of a triange intersects a segment
       crossing from the remaining side to the opposite vertex.  Theorem 3.17
       of [Schwabhauser], p. 33.  (Contributed by Scott Fenton,
       24-Sep-2013.) $)
    trisegint $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
       ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ P e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , C >. /\ E Btwn <. D , C >. /\ P Btwn <. A , D >. ) ->
       E. q e. ( EE ` N ) ( q Btwn <. P , C >. /\ q Btwn <. B , E >. ) ) ) $=
      ( vr wcel w3a cop cbtwn wbr wa wrex 3jca jca sylc 3ad2ant1 cee cfv simpl1
      cn simpl23 simpl21 simpl31 simpl32 simpl33 simpr2 btwncom syl13anc simpr3
      cv mpbid axpasch simp1l1 simp2 simpl22 simp3l simp1r1 simpll1 syl simpll2
      wb simplr simpl3r anim1i btwnexch2 anim1d reximdva mpd 3expia rexlimdva
      ex ) GUDJZAGUAUBZJZBVQJZCVQJZKZDVQJZFVQJZEVQJZKZKZBACLMNZFDCLMNZEADLMNZKZ
      HUNZECLZMNZWKBFLMNZOZHVQPZWFWJOZIUNZFALMNZWRWLMNZOZIVQPZWPWQVPVTVRWBKZWCW
      DOZKFCDLMNZWIOXBWQVPXCXDVPWAWEWJUCZWQVTVRWBVRVSVTVPWEWJUEZVRVSVTVPWEWJUFZ
      WBWCWDVPWAWJUGZQWQWCWDWBWCWDVPWAWJUHZWBWCWDVPWAWJUIZRQWQXEWIWQWHXEWFWGWHW
      IUJWQVPWCWBVTWHXEVEXFXJXIXGFDCGUKULUOWFWGWHWIUMRICADFEGUPSWQXAWPIVQWQWRVQ
      JZXAWPWQXLXAKZWKWRCLMNZWNOZHVQPZWPXMVPWCVTVRKZXLVSOZKWSBCALMNZOXPXMVPXQXR
      VPWAWEWJXLXAUQZXMWCVTVRWQXLWCXAXJTWQXLVTXAXGTZWQXLVRXAXHTZQXMXLVSWQXLXAUR
      WQXLVSXAVRVSVTVPWEWJUSTZRQXMWSXSWQXLWSWTUTXMWGXSWGWHWIWFXLXAVAXMVPVSVRVTW
      GXSVEXTYCYBYABACGUKULUORHFCAWRBGUPSXMXOWOHVQXMWKVQJZOZXNWMWNYEXNWMYEXNOZV
      PWDXLOZYDVTOZKWTXNOWMYFVPYGYHYFWQVPWQXLXAYDXNVBZXFVCYFWDXLYFWQWDYIXKVCWQX
      LXAYDXNVDRYFYDVTXMYDXNVFYFWQVTYIXGVCRQYEWTXNWSWTWQXLYDVGVHEWRWKCGVISVOVJV
      KVLVMVNVLVO $.
      $( [14-Oct-2013] $) $( [24-Sep-2013] $)
  $}

