$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Betweenness properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x $.  $d B x $.  $d N x $.
    $( Betweeness always holds for the second endpoint.  (Contributed by Scott
       Fenton, 12-Jun-2013.) $)
    btwntriv2 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
      B Btwn <. A , B >. ) $=
      ( vx cn wcel cee cfv w3a cv cop cbtwn wbr ccgr wa wrex simp1 simp2 simp3
      axsegcon syl122anc wceq wi simpl1 simpl3 simpr axcgrid syl13anc opeq2
      breq2d biimprd syl6 imp3a ancomsd r19.23adva mpd ) CEFZACGHZFZBURFZIZBADJ
      ZKZLMZBVBKBBKNMZOZDURPZBABKZLMZVAUQUSUTUTUTVGUQUSUTQUQUSUTRUQUSUTSZVJVJDA
      BBBCTUAVAVFVIDURVAVBURFZOZVEVDVIVLVEVDVIVLVEBVBUBZVDVIUCVLUQUTVKUTVEVMUCU
      QUSUTVKUDUQUSUTVKUEZVAVKUFVNBVBBCUGUHVMVIVDVMVHVCBLBVBAUIUJUKULUMUNUOUP
      $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.
    $( Betweeness commutes.  Implication version.  (Contributed by Scott
       Fenton, 12-Jun-2013.) $)
    btwncomim $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Btwn <. B , C >. -> A Btwn <. C , B >. ) ) $=
      ( vx cn wcel cee cfv w3a wa cop cbtwn wbr cv wrex btwntriv2 3adant3r2 wi
      simpl simpr2 simpr1 simpr3 axpasch syl132anc mpan2d wceq simpll simpr
      simplr1 axbtwnid syl111anc breq1 biimpd syl6 imp3a r19.23adva syld ) DFGZ
      ADHIZGZBUTGZCUTGZJZKZABCLMNZEOZAALMNZVGCBLZMNZKZEUTPZAVIMNZVEVFCACLMNZVLU
      SVAVCVNVBACDQRVEUSVBVAVCVAVCVFVNKVLSUSVDTUSVAVBVCUAUSVAVBVCUBZUSVAVBVCUCZ
      VOVPEBACACDUDUEUFVEVKVMEUTVEVGUTGZKZVHVJVMVRVHVGAUGZVJVMSVRUSVQVAVHVSSUSV
      DVQUHVEVQUIVAVBVCUSVQUJVGADUKULVSVJVMVGAVIMUMUNUOUPUQUR $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  $( Betweeness commutes.  (Contributed by Scott Fenton, 12-Jun-2013.) $)
  btwncom $p |- ( ( N e. NN /\
      ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
      ( A Btwn <. B , C >. <-> A Btwn <. C , B >. ) ) $=
    ( cn wcel cee cfv w3a wa cop cbtwn wbr btwncomim wi 3ancomb sylan2b impbid
    ) DEFZADGHZFZBTFZCTFZIZJABCKLMZACBKLMZABCDNUDSUAUCUBIUFUEOUAUBUCPACBDNQR $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Betweeness always holds for the first endpoint.  (Contributed by Scott
     Fenton, 12-Jun-2013.) $)
  btwntriv1 $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
      A Btwn <. A , B >. ) $=
    ( cn wcel cee cfv w3a cop cbtwn wbr btwntriv2 3com23 wb simp1 simp2 simp3
    btwncom syl13anc mpbird ) CDEZACFGZEZBUBEZHZAABIJKZABAIJKZUAUDUCUGBACLMUEUA
    UCUCUDUFUGNUAUCUDOUAUCUDPZUHUAUCUDQAABCRST $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.
    $( If you can swap the first two arguments of a betweenness statement, then
       those arguments are identical.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnswapid $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
       ( ( A Btwn <. B , C >. /\ B Btwn <. A , C >. ) ->
         A = B ) ) $=
      ( vx cn wcel cee cfv w3a wa cop cbtwn wbr cv wrex wceq wi simpl simpr2
      simpr1 simpr3 axpasch syl132anc simpll simpr simplr1 axbtwnid syl111anc
      simplr2 anim12d eqtr2 syl6 r19.23adva syld ) DFGZADHIZGZBUQGZCUQGZJZKZABC
      LMNBACLMNKZEOZAALMNZVDBBLMNZKZEUQPZABQZVBUPUSURUTURUSVCVHRUPVASUPURUSUTTZ
      UPURUSUTUAZUPURUSUTUBVKVJEBACABDUCUDVBVGVIEUQVBVDUQGZKZVGVDAQZVDBQZKVIVMV
      EVNVFVOVMUPVLURVEVNRUPVAVLUEZVBVLUFZURUSUTUPVLUGVDADUHUIVMUPVLUSVFVORVPVQ
      URUSUTUPVLUJVDBDUHUIUKVDABULUMUNUO $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.  $d C x $.  $d D x $.
    $( Inner transitivity law for betweenness.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnintr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , D >. /\ C Btwn <. B , D >. ) ->
         B Btwn <. A , C >. ) ) $=
      ( vx cn wcel cee cfv wa w3a cop cbtwn wbr cv wrex wi simp1 simp2l simp2r
      simp3r simp3l axpasch syl132anc wceq simpl1 simpr simpl2r axbtwnid
      syl111anc breq1 biimpa wb simpl3l simpl2l btwncom syl13anc biimpd syl5
      exp3a syld imp3a r19.23adva ) EGHZAEIJZHZBVFHZKZCVFHZDVFHZKZLZBADMNOCBDMN
      OKZFPZBBMNOZVOCAMZNOZKZFVFQZBACMNOZVMVEVGVHVKVHVJVNVTRVEVIVLSVEVGVHVLTVEV
      GVHVLUAZVEVIVJVKUBWBVEVIVJVKUCFABDBCEUDUEVMVSWAFVFVMVOVFHZKZVPVRWAWDVPVOB
      UFZVRWARWDVEWCVHVPWERVEVIVLWCUGZVMWCUHVGVHVEVLWCUIZVOBEUJUKWDWEVRWAWEVRKB
      VQNOZWDWAWEVRWHVOBVQNULUMWDWHWAWDVEVHVJVGWHWAUNWFWGVJVKVEVIWCUOVGVHVEVLWC
      UPBCAEUQURUSUTVAVBVCVDVB $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)

    $( Exchange the first endpoint in betweenness.  (Contributed by Scott
       Fenton, 12-Jun-2013.) $)
    btwnexch3 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B Btwn <. A , C >. /\ C Btwn <. A , D >. ) ->
         C Btwn <. B , D >. ) ) $=
      ( vx cn wcel cee cfv wa w3a cop cbtwn wbr cv wrex wb simp1 simp3l simp2l
      simp3r btwncom syl13anc simp2r anbi12d wi axpasch syl132anc sylbid
      ancomsd wceq simpl1 simpr simpl3l axbtwnid syl111anc breq1 biimpd syl6
      imp3a r19.23adva syld ) EGHZAEIJZHZBVEHZKZCVEHZDVEHZKZLZBACMNOZCADMNOZKFP
      ZCCMNOZVOBDMZNOZKZFVEQZCVQNOZVLVNVMVTVLVNVMKCDAMNOZBCAMNOZKZVTVLVNWBVMWCV
      LVDVIVFVJVNWBRVDVHVKSZVDVHVIVJTZVDVFVGVKUAZVDVHVIVJUBZCADEUCUDVLVDVGVFVIV
      MWCRWEVDVFVGVKUEZWGWFBACEUCUDUFVLVDVJVIVFVIVGWDVTUGWEWHWFWGWFWIFDCACBEUHU
      IUJUKVLVSWAFVEVLVOVEHZKZVPVRWAWKVPVOCULZVRWAUGWKVDWJVIVPWLUGVDVHVKWJUMVLW
      JUNVIVJVDVHWJUOVOCEUPUQWLVRWAVOCVQNURUSUTVAVBVC $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)

    $( Outer transitivity law for betweenness.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    btwnouttr2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
       ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
       ( ( B =/= C /\
           B Btwn <. A , C >. /\ C Btwn <. B , D >. ) ->
         C Btwn <. A , D >. ) ) $=
      ( vx cn wcel cee cfv wa w3a wne cop cbtwn wbr cv ccgr wrex simp1 simp2l
      simp3l simp3r axsegcon syl122anc adantr wi simprrl wceq simprl1 simpl2
      simprl jca adantl simpl1 simpl2l simpl2r simpl3l simpr btwnexch3 mpd
      simprrr simprl3 simpl3r cgrrflx syl111anc 3jca segconeq syl133anc opeq2d
      breqtrd expr an32s r19.23adva ex ) EGHZAEIJZHZBVQHZKZCVQHZDVQHZKZLZBCMZBA
      CNOPZCBDNOPZLZCADNZOPZWDWHKZCAFQZNZOPZCWLNCDNZRPZKZFVQSZWJWDWRWHWDVPVRWAW
      AWBWRVPVTWCTVPVRVSWCUAVPVTWAWBUBZWSVPVTWAWBUCFACCDEUDUEUFWKWQWJFVQWDWLVQH
      ZWHWQWJUGWDWTKZWHWQWJXAWHWQKZKZCWMWIOXAWHWNWPUHXCWLDAXCWECBWLNOPZWPKZWGWO
      WORPZKZLZWLDUIZXCWEXEXGWEWFWGWQXAUJXCXDWPXCWFWNKZXDXBXJXAXBWFWNWEWFWGWQUK
      WHWNWPULUMUNXAXJXDUGZXBXAVPVRVSWAWTXKVPVTWCWTUOZVRVSVPWCWTUPVRVSVPWCWTUQZ
      WAWBVPVTWTURZWDWTUSZABCWLEUTUEUFVAXAWHWNWPVBUMXCWGXFWEWFWGWQXAVCXAXFXBXAV
      PWAWBXFXLXNWAWBVPVTWTVDZCDEVEVFUFUMVGXAXHXIUGZXBXAVPWAWAWBVSWTWBXQXLXNXNX
      PXMXOXPCCDBEWLDVHVIUFVAVJVKVLVMVNVAVO $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  $( Exchange the outer point of two betweenness statements.  (Contributed by
     Scott Fenton, 14-Jun-2013.) $)
  btwnexch2 $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
   ( ( B Btwn <. A , D >. /\ C Btwn <. B , D >. ) ->
     C Btwn <. A , D >. ) ) $=
    ( cn wcel cee cfv wa w3a cop cbtwn wbr wi wceq breq1 biimpd adantrd a1i
    wne simprl simprr btwnintr adantr mpd simprrr 3jca btwnouttr2 exp32
    pm2.61dne ) EFGAEHIZGBULGJCULGDULGJKZBADLZMNZCBDLMNZJZCUNMNZOZBCBCPZUSOUMUT
    UOURUPUTUOURBCUNMQRSTUMBCUAZUQURUMVAUQJZJZVABACLMNZUPKZURVCVAVDUPUMVAUQUBVC
    UQVDUMVAUQUCUMUQVDOVBABCDEUDUEUFUMVAUOUPUGUHUMVEUROVBABCDEUIUEUFUJUK $.
    $( [16-Sep-2013] $) $( [14-Jun-2013] $)

  $( Outer transitivity law for betweenness.  (Contributed by Scott Fenton,
     14-Jun-2013.) $)
  btwnouttr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
   ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
   ( ( B =/= C /\
       B Btwn <. A , C >. /\ C Btwn <. B , D >. ) ->
     B Btwn <. A , D >. ) ) $=
    ( cn wcel cee cfv wa w3a wne cop cbtwn wbr wb necom a1i simp1 simp2r
    simp2l simp3l btwncom syl13anc simp3r 3anbi123d 3ancomb syl6bb biimpa wi
    btwnouttr2 syl122anc adantr mpd simpl1 simpl2r simpl2l simpl3r mpbird ex )
    EFGZAEHIZGZBVBGZJZCVBGZDVBGZJZKZBCLZBACMNOZCBDMNOZKZBADMNOZVIVMJZVNBDAMNOZV
    OCBLZCDBMNOZBCAMNOZKZVPVIVMVTVIVMVQVSVRKVTVIVJVQVKVSVLVRVJVQPVIBCQRVIVAVDVC
    VFVKVSPVAVEVHSZVAVCVDVHTZVAVCVDVHUAZVAVEVFVGUBZBACEUCUDVIVAVFVDVGVLVRPWAWDW
    BVAVEVFVGUEZCBDEUCUDUFVQVSVRUGUHUIVIVTVPUJZVMVIVAVGVFVDVCWFWAWEWDWBWCDCBAEU
    KULUMUNVOVAVDVCVGVNVPPVAVEVHVMUOVCVDVAVHVMUPVCVDVAVHVMUQVFVGVAVEVMURBADEUCU
    DUSUT $.
    $( [16-Sep-2013] $) $( [14-Jun-2013] $)

$( (End of Scott Fenton's mathbox.) $)


