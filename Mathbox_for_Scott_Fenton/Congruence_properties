$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Congruence properties
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Reflexivity law for congruence.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrrflx $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    <. A , B >. Cgr <. A , B >. ) $=
    ( cn wcel cee cfv w3a cop ccgr wbr axcgrrflx 3com23 wa wi simp1 simp3
    simp2 axcgrtr syl133anc mp2and ) CDEZACFGZEZBUCEZHZBAIABIZJKZUHUGUGJKZUBUEU
    DUHBACLMZUJUFUBUEUDUDUEUDUEUHUHNUIOUBUDUEPUBUDUEQZUBUDUERZULUKULUKBAABABCST
    UA $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Congruence commutes on the two sides.  Implication version.  (Contributed
     by Scott Fenton, 12-Jun-2013.) $)
  cgrcomim $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. ->
      <. C , D >. Cgr <. A , B >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr cgrrflx 3expb 3adant3 wi simp1
    simp2l simp2r simp3l simp3r axcgrtr syl133anc mpan2d ) EFGZAEHIZGZBUHGZJZCU
    HGZDUHGZJZKZABLZCDLZMNZUPUPMNZUQUPMNZUGUKUSUNUGUIUJUSABEOPQUOUGUIUJULUMUIUJ
    URUSJUTRUGUKUNSUGUIUJUNTZUGUIUJUNUAZUGUKULUMUBUGUKULUMUCVAVBABCDABEUDUEUF
    $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Congruence commutes between the two sides.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcom $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. C , D >. Cgr <. A , B >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr cgrcomim wi 3com23 impbid ) EFGZAEHIZ
    GBTGJZCTGDTGJZKABLZCDLZMNZUDUCMNZABCDEOSUBUAUFUEPCDABEOQR $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Congruence commutes on the left.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcoml $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. B , A >. Cgr <. C , D >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr simp1 simp2l simp2r axcgrrflx
    syl111anc wi simp3l simp3r axcgrtr syl133anc mpand impbid ) EFGZAEHIZGZBUHG
    ZJZCUHGZDUHGZJZKZABLZCDLZMNZBALZUQMNZUOUPUSMNZURUTUOUGUIUJVAUGUKUNOZUGUIUJU
    NPZUGUIUJUNQZABERSUOUGUIUJUJUIULUMVAURJUTTVBVCVDVDVCUGUKULUMUAZUGUKULUMUBZA
    BBACDEUCUDUEUOUSUPMNZUTURUOUGUJUIVGVBVDVCBAERSUOUGUJUIUIUJULUMVGUTJURTVBVDV
    CVCVDVEVFBAABCDEUCUDUEUF $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Congruence commutes on the right.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcomr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. A , B >. Cgr <. D , C >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr wb cgrcoml 3com23 cgrcom simp1
    simp2l simp2r simp3r simp3l syl122anc 3bitr4d ) EFGZAEHIZGZBUGGZJZCUGGZDUGG
    ZJZKZCDLZABLZMNZDCLZUPMNZUPUOMNUPURMNZUFUMUJUQUSOCDABEPQABCDERUNUFUHUIULUKU
    TUSOUFUJUMSUFUHUIUMTUFUHUIUMUAUFUJUKULUBUFUJUKULUCABDCERUDUE $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)


  $( Congruence commutes on both sides.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrcomlr $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
    ( <. A , B >. Cgr <. C , D >. <->
      <. B , A >. Cgr <. D , C >. ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr cgrcoml wb ancom cgrcomr syl3an2b
    bitrd ) EFGZAEHIZGZBUBGZJZCUBGDUBGJZKABLCDLZMNBALZUGMNZUHDCLMNZABCDEOUEUAUD
    UCJUFUIUJPUCUDQBACDERST $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  ${
    $d A x $.  $d B x $.  $d N x $.
    $( Degenerate segments are congruent.  (Contributed by Scott Fenton,
       12-Jun-2013.) $)
    cgrtriv $p |- ( ( N e. NN /\ A e. ( EE ` N ) /\ B e. ( EE ` N ) ) ->
    <. A , A >. Cgr <. B , B >. ) $=
      ( vx cn wcel cee cfv w3a cv cop cbtwn wbr ccgr wa wrex simp1 simp2 simp3
      axsegcon syl122anc wceq wi simpl1 simpl2 simpr simpl3 axcgrid syl13anc
      opeq2 breq1d biimprd syli adantld r19.23adva mpd ) CEFZACGHZFZBURFZIZAADJ
      ZKZLMZVCBBKZNMZOZDURPZAAKZVENMZVAUQUSUSUTUTVHUQUSUTQUQUSUTRZVKUQUSUTSZVLD
      AABBCTUAVAVGVJDURVAVBURFZOZVFVJVDVFVNAVBUBZVJVNUQUSVMUTVFVOUCUQUSUTVMUDUQ
      USUTVMUEVAVMUFUQUSUTVMUGAVBBCUHUIVOVJVFVOVIVCVENAVBAUJUKULUMUNUOUP $.
      $( [16-Sep-2013] $) $( [12-Jun-2013] $)
  $}

  $( Identity law for congruence.  (Contributed by Scott Fenton,
     12-Jun-2013.) $)
  cgrid2 $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) ) ->
    ( <. A , A >. Cgr <. B , C >. -> B = C ) ) $=
    ( cn wcel cee cfv w3a wa cop ccgr wbr wceq wb simpl simpr1 simpr2 simpr3
    cgrcom syl122anc wi 3anrot axcgrid sylan2b sylbid ) DEFZADGHZFZBUHFZCUHFZIZ
    JZAAKZBCKZLMZUOUNLMZBCNZUMUGUIUIUJUKUPUQOUGULPUGUIUJUKQZUSUGUIUJUKRUGUIUJUK
    SAABCDTUAULUGUJUKUIIUQURUBUIUJUKUCBCADUDUEUF $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Two congruent segments are either both degenrate or both non-degenerate.
     (Contributed by Scott Fenton, 12-Jun-2013.) $)
  cgrdegen $p |- ( ( N e. NN /\ ( A e. ( EE ` N ) /\ B e. ( EE ` N ) ) /\
    ( C e. ( EE ` N ) /\ D e. ( EE ` N ) ) ) ->
      ( <. A , B >. Cgr <. C , D >. -> ( A = B <-> C = D ) ) ) $=
    ( cn wcel cee cfv wa w3a cop ccgr wbr wceq wb opeq1 breq1d biimpac wi
    simp1 simp2r simp3l simp3r cgrid2 syl13anc syl5 expdimp breq2d simp2l
    axcgrid impbid ex ) EFGZAEHIZGZBUOGZJZCUOGZDUOGZJZKZABLZCDLZMNZABOZCDOZPVBV
    EJVFVGVBVEVFVGVEVFJBBLZVDMNZVBVGVFVEVIVFVCVHVDMABBQRSVBUNUQUSUTVIVGTUNURVAU
    AZUNUPUQVAUBZUNURUSUTUCUNURUSUTUDZBCDEUEUFUGUHVBVEVGVFVEVGJVCDDLZMNZVBVFVGV
    EVNVGVDVMVCMCDDQUISVBUNUPUQUTVNVFTVJUNUPUQVAUJVKVLABDEUKUFUGUHULUM $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Link congruence over a pair of line segments.  (Contributed by Scott
     Fenton, 12-Jun-2013.) $)
  cgrextend $p |- ( ( N e. NN /\
    ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
    ( D e. ( EE ` N ) /\ E e. ( EE ` N ) /\ F e. ( EE ` N ) ) ) ->
    ( ( ( B Btwn <. A , C >. /\ E Btwn <. D , F >. ) /\
        ( <. A , B >. Cgr <. D , E >. /\
          <. B , C >. Cgr <. E , F >. ) ) ->
      <. A , C >. Cgr <. D , F >. ) ) $=
    ( cn wcel cee cfv w3a cop cbtwn wbr wa ccgr wi wceq simp1 simp22 simp31
    simp32 3jca jca simprl cgrid2 imp syl2an simprrr opeq1 breq2d biimpar
    syl11anc ex anbi1d breq1d anbi12d imbi12d imbi2d mpbiri wne simprll adantl
    simprlr simp21 cgrtriv syl111anc adantr simprrl wb cgrcomlr syl122anc
    mpbid simpl1 simpl21 simpl22 simpl23 simpl31 simpl32 simpl33 ax5seg
    syl333anc mpd simp23 simp33 mpbird exp3a com12 pm2.61ine ) GHIZAGJKZIZBWLIZ
    CWLIZLZDWLIZEWLIZFWLIZLZLZBACMZNOZEDFMZNOZPZABMZDEMZQOZBCMZEFMZQOZPZPZXBXDQ
    OZRZRZABABSZXQXABXJNOZXEPZBBMZXHQOZXLPZPZXJXDQOZRZRXAYDYEXAYDPDESZXLYEXAWKW
    NWQWRLZPZYBYGYDXAWKYHWKWPWTTZXAWNWQWRWKWMWNWOWTUAZWKWPWQWRWSUBZWKWPWQWRWSUC
    ZUDUEXTYBXLUFYIYBYGBDEGUGUHUIXAXTYBXLUJYGYEXLYGXDXKXJQDEFUKULUMUNUOXRXPYFXA
    XRXNYDXOYEXRXFXTXMYCXRXCXSXEXRXBXJBNABCUKZULUPXRXIYBXLXRXGYAXHQABBUKUQUPURX
    RXBXJXDQYNUQUSUTVAXAABVBZXPXAYOXNXOXAYOXNPZXOXAYPPZXOCAMFDMQOZYQYOXCXELZXMA
    AMDDMQOZBAMEDMQOZPZLZYRYQYSXMUUBYQYOXCXEXAYOXNUFYPXCXAYOXCXEXMVCVDYPXEXAYOX
    CXEXMVEVDUDXAYOXFXMUJYQYTUUAXAYTYPXAWKWMWQYTYJWKWMWNWOWTVFZYLADGVGVHVIYQXIU
    UAYPXIXAYOXFXIXLVJVDXAXIUUAVKZYPXAWKWMWNWQWRUUEYJUUDYKYLYMABDEGVLVMVIVNUEUD
    YQWKWMWNWOWMWQWRWSWQUUCYRRWKWPWTYPVOWMWNWOWKWTYPVPZWMWNWOWKWTYPVQWMWNWOWKWT
    YPVRUUFWQWRWSWKWPYPVSZWQWRWSWKWPYPVTWQWRWSWKWPYPWAUUGABCADEFDGWBWCWDXAXOYRV
    KZYPXAWKWMWOWQWSUUHYJUUDWKWMWNWOWTWEYLWKWPWQWRWSWFACDFGVLVMVIWGUOWHWIWJ $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

  $( Two points that satsify the conclusion of ~ axsegcon are identical.
     (Contributed by Scott Fenton, 12-Jun-2013.) $)
  segconeq $p |- ( ( N e. NN /\
   ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ C e. ( EE ` N ) ) /\
   ( Q e. ( EE ` N ) /\ X e. ( EE ` N ) /\ Y e. ( EE ` N ) ) ) ->
   ( ( Q =/= A /\
     ( A Btwn <. Q , X >. /\ <. A , X >. Cgr <. B , C >. ) /\
     ( A Btwn <. Q , Y >. /\ <. A , Y >. Cgr <. B , C >. ) )
   -> X = Y ) ) $=
    ( cn wcel cee cfv w3a wne cop cbtwn wbr ccgr wa wceq simpr1 simpr2l 3jca
    simp1 simp31 simp21 cgrrflx syl111anc simp32 jca adantr simpr3l wb simp33
    simp22 simp23 cgrcom syl122anc anbi12d simpr3r simpr2r mpbi2and wi axcgrtr
    syl133anc mpd cgrextend mp2and ax5seg syl333anc axcgrid syl13anc ex ) EHIZA
    EJKZIZBVNIZCVNIZLZDVNIZFVNIZGVNIZLZLZDAMZADFNZOPZAFNZBCNZQPZRZADGNZOPZAGNZW
    HQPZRZLZFGSZWCWPRZFGNFFNQPZWQWRWDWFWFLZDANZXAQPZWGWGQPZRZWKWEQPZWMWGQPZRZLZ
    WSWRWTXDXGWRWDWFWFWCWDWJWOTWFWIWDWOWCUAZXIUBWCXDWPWCXBXCWCVMVSVOXBVMVRWBUCZ
    VMVRVSVTWAUDZVMVOVPVQWBUEZDAEUFUGZWCVMVOVTXCXJXLVMVRVSVTWAUHZAFEUFUGUIUJWRX
    EXFWRWLWFRZXBXFRZXEWRWLWFWLWNWDWJWCUKXIUIWRXBXFWCXBWPXMUJWRWHWMQPZWHWGQPZRZ
    XFWRWNWIXSWCWNWIRXSULWPWCWNXQWIXRWCVMVOWAVPVQWNXQULXJXLVMVRVSVTWAUMZVMVOVPV
    QWBUNZVMVOVPVQWBUOZAGBCEUPUQWCVMVOVTVPVQWIXRULXJXLXNYAYBAFBCEUPUQURUJWLWNWD
    WJWCUSWFWIWDWOWCUTVAWCXSXFVBZWPWCVMVPVQVOWAVOVTYCXJYAYBXLXTXLXNBCAGAFEVCVDU
    JVEZUIWCXOXPRXEVBZWPWCVMVSVOWAVSVOVTYEXJXKXLXTXKXLXNDAGDAFEVFVDUJVGYDUIUBWC
    XHWSVBZWPWCVMVSVOVTWAVSVOVTVTYFXJXKXLXNXTXKXLXNXNDAFGDAFFEVHVIUJVEWCWSWQVBZ
    WPWCVMVTWAVTYGXJXNXTXNFGFEVJVKUJVEVL $.
    $( [16-Sep-2013] $) $( [12-Jun-2013] $)

