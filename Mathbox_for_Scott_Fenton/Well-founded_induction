$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Well-founded induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x z w t u v $.  $d A y $.  $d B x z w t u v $.  $d B y $.
    $d R x z w t u v $.  $d R y $.  $d y z w t u v $.
    $( All nonempty (possibly proper) subclasses of ` A ` , which has a
       well-founded relation ` R ` , have ` R `-minimal elements.  Proposition
       6.26 of [TakeutiZaring] p. 31. $)
    tz6.26 $p |- ( ( ( R We A /\
                        A. x e. A Pred ( R , A , x ) e. _V ) /\
                      ( B C_ A /\ B =/= (/) ) ) ->
                    E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( vt vz vw wwe cv cpred cvv wcel wral wa wss c0 wne wceq wrex wi wess
      ssralv predpredss ssexg ex syl ralimdv syld cbvsetlike syl5ib anim12d
      wex weq predeq3 eqeq1d rcla4ev adantl predss wfr wefr eleq1d rcla4cva
      anim12i anassrs sseq1 neeq1 anbi12d predeq2 rexeqbi1dv imbi12d imbi2d
      wal dffr4 ax-4 sylbi vtoclg impcom mpani wor wbr weso anim1i wb visset
      elpred ax-mp biimpi w3a sotr 3exp2 com23 com34 3imp exp3a 3exp imp43
      elpredg adantll adantlr 3adant2 mpbird imdistand ancom bitri 3imtr4g
      ssrdv sseq0 syl2an reximdva ssrexv syl6 pm2.61dne 19.23adv n0 syl6com
      imp32 ) CEIZCEAJKLMACNZOZDCPZDQRZDEBJZKZQSZBDTZYAXTDEIZDEFJZKZLMZFDNZOZYB
      YFUAYAXRYGXSYKDCEUBYACEYHKZLMZFCNZYKXSYAYOYNFDNYKYNFDCUCYAYNYJFDYAYIYMPZY
      NYJUADCEYHUDYPYNYJYIYMLUEUFUGUHUIAFCEUJUKULYLGJZDMZGUMYFYBYLYRYFGYLYRYFYL
      YROZYFDEYQKZQYRYTQSZYFUAYLYRUUAYFYEUUABYQDBGUNYDYTQDEYCYQUOUPUQUFURYSYTQR
      ZYEBYTTZYFYSUUBYTEYCKZQSZBYTTZUUCYSYTDPZUUBUUFDEYQUSZYSDEUTZYTLMZOZUUGUUB
      OZUUFUAZYGYKYRUUKYGUUIYKYROUUJDEVAYJUUJFYQDFGUNYIYTLDEYHYQUOVBVCVDVEUUJUU
      IUUMUUIHJZDPZUUNQRZOZUUNEYCKZQSZBUUNTZUAZUAUUIUUMUAHYTLUUNYTSZUVAUUMUUIUV
      BUUQUULUUTUUFUVBUUOUUGUUPUUBUUNYTDVFUUNYTQVGVHUUSUUEBUUNYTUVBUURUUDQUUNYT
      EYCVIUPVJVKVLUUIUVAHVMUVAHBDEVNUVAHVOVPVQVRUGVSYGYRUUFUUCUAYKYGYROZUUEYEB
      YTUVCDEVTZYROZYCDMZYCYQEWAZOZUUEYEUAZYCYTMZYGUVDYRDEWBWCUVJUVHYQLMUVJUVHW
      DGWEDLEYQYCBWEZWFWGWHUVEUVHOZYDUUDPZUVIUVLHYDUUDUVLUUNYCEWAZUUNDMZOZUVNUU
      NYTMZOZUUNYDMZUUNUUDMZUVLUVNUVOUVQUVLUVNUVOUVQUVLUVNUVOWIUVQUUNYQEWAZUVLU
      VNUVOUWAUVDYRUVFUVGUVNUVOUWAUAZUAZUVDUVFYRUVGUWCUAZUVDUVFYRUWDUVDUVFYRWIZ
      UVNUVGUWBUWEUVNUVGUWBUWEUVOUVNUVGOZUWAUVDUVFYRUVOUWFUWAUAZUAUVDUVFUVOYRUW
      GUVDUVOUVFYRUWGUAUVDUVOUVFYRUWGDUUNYCYQEWJWKWLWMWNWLWOWLWPWLWQWNUVLUVOUVQ
      UWAWDZUVNUVEUVOUWHUVHYRUVOUWHUVDDDEYQUUNWRWSWTXAXBWPXCUVSUVOUVNOZUVPYCLMZ
      UVSUWIWDUVKDLEYCUUNHWEZWFWGUVOUVNXDXEUVTUVQUVNOZUVRUWJUVTUWLWDUVKYTLEYCUU
      NUWKWFWGUVQUVNXDXEXFXGUVMUUEYEYDUUDXHUFUGXIXJWTUIUUGUUCYFUAUUHYEBYTDXKWGX
      LXMUFXNGDXOUKXPXQ $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    tz6.26i.1 $e |- R We A $.
    tz6.26i.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    $( All nonempty (possibly proper) subclasses of ` A ` , which has a
       well-founded relation ` R ` , have ` R `-minimal elements.  Proposition
       6.26 of [TakeutiZaring] p. 31. $)
    tz6.26i $p |- ( ( B C_ A /\ B =/= (/) ) ->
                    E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( wwe cv cpred cvv wcel wral wss c0 wne wa wceq wrex tz6.26 mpanl12 ) CEH
      CEAIJKLACMDCNDOPQDEBIJORBDSFGABCDETUA $.
      $( [14-Apr-2011] $)
  $}


  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    $( The Principle of Well-Founded Induction.  Theorem 6.27 of
       [TakeutiZaring] p. 32.  This principle states that if ` B ` is a
       subclass of a well-ordered class ` A ` with the property that every
       element of ` B ` whose inital segment is included in ` A ` is itself
       equal to ` A ` . $)
    wfi $p |- ( ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                    ( B C_ A /\
                      A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) )
                  -> A = B ) $=
      ( wwe cv cpred cvv wcel wral wa wss wi cdif c0 wne wn difss wceq wrex
      tz6.26 eldif anbi1i anass ancom ccnv csn cima cin indif2 df-pred incom
      eqtri difeq1i 3eqtr4i eqeq1i ssdif0 bitr4i bitri anbi2i 3bitri rexbii2
      rexanali sylib ex mpani necon3bbii syl5ib con4d imp adantrl simprl eqssd
      ) CEFCEAGHIJACKLZDCMZCEBGZHZDMZVQDJZNBCKZLLCDVOWACDMZVPVOWAWBVOWBWAVOCDOZ
      PQZWARZWBRVOWCCMZWDWECDSVOWFWDLZWEVOWGLWCEVQHZPTZBWCUAZWEABCWCEUBWJVSVTRZ
      LZBCUAWEWIWLBWCCVQWCJZWILVQCJZWKLZWILWNWKWILZLWNWLLWMWOWIVQCDUCUDWNWKWIUE
      WPWLWNWPWIWKLWLWKWIUFWIVSWKWIVRDOZPTVSWHWQPEUGVQUHUIZWCUJZWRCUJZDOWHWQWRC
      DUKWHWCWRUJWSWCEVQULWCWRUMUNVRWTDVRCWRUJWTCEVQULCWRUMUNUOUPUQVRDURUSUDUTV
      AVBVCVSVTBCVDUTVEVFVGWBWCPCDURVHVIVJVKVLVOVPWAVMVN $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    wfi.1 $e |- R We A $.
    wfi.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    $( The Principle of Well-Founded Induction.  Theorem 6.27 of
       [TakeutiZaring] p. 32.  This principle states that if ` B ` is a
       subclass of a well-ordered class ` A ` with the property that every
       element of ` B ` whose inital segment is included in ` A ` is itself
       equal to ` A ` . $)
    wfii $p |- ( ( B C_ A /\
                    A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) )
                  -> A = B ) $=
      ( wwe cv cpred cvv wcel wral wa wss wi wceq pm3.2i wfi mpan ) CEHZCEAIJKL
      ACMZNDCOCEBIZJDOUCDLPBCMNCDQUAUBFGRABCDEST $.
      $( [29-Jan-2011] $)
  $}


  ${
    $d A w x y $.  $d A z $.  $d ph w x $.  $d ph z $.  $d R w x y $.
    $d R z $.  $d w y z $.
    wfisg.1 $e |- ( y e. A ->
                       ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Well-Founded Induction Schema.  If a property passes from all elements
       less than ` y ` of a well founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` . $)
    wfisg $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( vw wwe cv cpred cvv wcel wral wa crab wceq wss wi ssrab2 wsbc ax-17
      hbs1 hbral hbim eleq1 predeq3 raleqdv sbequ12 imbi12d chvar dfss3
      elrabsf simprbi ralimi sylbi syl5 anc2li syl6ibr rgen wfi mpanr12 rabid2
      sylib ) EFIEFBJKLMBENOZEACEPZQZACENVEVFEREFHJZKZVFRZVHVFMZSZHENVGACETVLHE
      VHEMZVJVMACVHUAZOVKVMVJVNVMACDJZUAZDVINZVNVJCJZEMZVPDEFVRKZNZASZSVMVQVNSZ
      SCHVMWCCVMCUBZVQVNCVPCDVIVOVIMCUBACDUCUDACHUCUEUEVRVHQZVSVMWBWCVRVHEUFWEW
      AVQAVNWEVPDVTVIEFVRVHUGUHACHUIUJUJGUKVJVOVFMZDVINVQDVIVFULWFVPDVIWFVOEMVP
      ACHVOEWDUMUNUOUPUQURACHVHEWDUMUSUTBHEVFFVAVBACEVCVD $.
      $( [11-Feb-2011] $)
  $}


  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d ph x $.  $d R w y z $.
    $d R x $.  $d x y $.
    wfis.1 $e |- R We A $.
    wfis.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis.3 $e |- ( y e. A ->
                    ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Well-Founded Induction Schema.  If all elements less than a given set
       ` x ` of the well founded class ` A ` have a property (induction
       hypothesis), then all elements of ` A ` have that property. $)
    wfis $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfisg mp2an rspec ) ACEEFJEFBKLMNBEOACEOGHAB
      CDEFIPQR $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d R x y $.  $d R z $.
    $d y z $.
    wfis2fg.1 $e |- ( ps -> A. y ps ) $.
    wfis2fg.2 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2fg.3 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well-Founded Induction Schema, using implicit substitution. $)
    wfis2fg $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( cv wcel cpred wral wsbc sbie ralbii syl5ib wfisg ) ACDEFGDKZFLBEFGTMZNA
      ADEKOZEUANJUBBEUAABDEHIPQRS $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d R x y $.  $d R z $.
    $d y z $.
    wfis2f.1 $e |- R We A $.
    wfis2f.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis2f.3 $e |- ( ps -> A. y ps ) $.
    wfis2f.4 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2f.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well Founded Induction schema, using implicit substitution. $)
    wfis2f $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfis2fg mp2an rspec ) ADFFGMFGCNOPQCFRADFRHI
      ABCDEFGJKLSTUA $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d ps y $.  $d R x y $.
    $d R z $.  $d y z $.
    wfis2g.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2g.2 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well-Founded Induction Schema, using implicit substitution. $)
    wfis2g $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( ax-17 wfis2fg ) ABCDEFGBDJHIK $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d ps y $.  $d R x y $.
    $d R z $.  $d y z $.
    wfis2.1 $e |- R We A $.
    wfis2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis2.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2.4 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well Founded Induction schema, using implicit substitution. $)
    wfis2 $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfis2g mp2an rspec ) ADFFGLFGCMNOPCFQADFQHIA
      BCDEFGJKRST $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d B y $.  $d ch y $.  $d ph x $.  $d ph z $.
    $d ps y $.  $d R x y $.  $d R z $.  $d y z $.
    wfis3.1 $e |- R We A $.
    wfis3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis3.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis3.4 $e |- ( y = B -> ( ph <-> ch ) ) $.
    wfis3.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.


    $( Well Founded Induction schema, using implicit substitution. $)
    wfis3 $p |- ( B e. A -> ch ) $=
      ( wfis2 vtoclga ) ACEHGMABDEFGIJKLNOP $.
      $( [29-Jan-2011] $)
  $}

