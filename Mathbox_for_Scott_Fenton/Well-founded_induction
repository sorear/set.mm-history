$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Well-founded induction
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A x z w t u v $.  $d A y $.  $d B x z w t u v $.  $d B y $.
    $d R x z w t u v $.  $d R y $.  $d y z w t u v $.
    $( All nonempty (possibly proper) subclasses of ` A ` , which has a
       well-founded relation ` R ` , have ` R `-minimal elements.  Proposition
       6.26 of [TakeutiZaring] p. 31.  (Contributed by Scott Fenton,
       29-Jan-2011.) $)
    tz6.26 $p |- ( ( ( R We A /\
                        A. x e. A Pred ( R , A , x ) e. _V ) /\
                      ( B C_ A /\ B =/= (/) ) ) ->
                    E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( vt vz vw cv cpred cvv wcel wral wa wss c0 wceq wrex wi ex wwe wne ssexg
      wess cbvsetlike ssralv predpredss syl ralimdv syld syl5bi anim12d wex weq
      n0 predeq3 eqeq1d rcla4ev adantl predss wfr wefr rcla4cva anim12i anassrs
      eleq1d sseq1 neeq1 anbi12d predeq2 rexeqbi1dv imbi2d wal dffr4 ax-4 sylbi
      imbi12d vtoclg impcom mpani wor weso anim1i wb visset elpred ax-mp biimpi
      wbr w3a sotr 3exp2 com23 com34 3imp exp3a elpredg adantll adantlr 3adant2
      3exp imp43 mpbird imdistand ancom bitri 3imtr4g ssrdv sseq0 syl2an ssrexv
      reximdva syl6 pm2.61dne exlimdv syl6com imp32 ) CEUAZCEAIJKLACMZNZDCOZDPU
      BZDEBIZJZPQZBDRZYAXTDEUAZDEFIZJZKLZFDMZNZYBYFSYAXRYGXSYKDCEUDXSCEYHJZKLZF
      CMZYAYKAFCEUEYAYOYNFDMYKYNFDCUFYAYNYJFDYAYIYMOZYNYJSDCEYHUGYPYNYJYIYMKUCT
      UHUIUJUKULYBGIZDLZGUMYLYFGDUOYLYRYFGYLYRYFYLYRNZYFDEYQJZPYRYTPQZYFSYLYRUU
      AYFYEUUABYQDBGUNYDYTPDEYCYQUPUQURTUSYSYTPUBZYEBYTRZYFYSUUBYTEYCJZPQZBYTRZ
      UUCYSYTDOZUUBUUFDEYQUTZYSDEVAZYTKLZNZUUGUUBNZUUFSZYGYKYRUUKYGUUIYKYRNUUJD
      EVBYJUUJFYQDFGUNYIYTKDEYHYQUPVFVCVDVEUUJUUIUUMUUIHIZDOZUUNPUBZNZUUNEYCJZP
      QZBUUNRZSZSUUIUUMSHYTKUUNYTQZUVAUUMUUIUVBUUQUULUUTUUFUVBUUOUUGUUPUUBUUNYT
      DVGUUNYTPVHVIUUSUUEBUUNYTUVBUURUUDPUUNYTEYCVJUQVKVQVLUUIUVAHVMUVAHBDEVNUV
      AHVOVPVRVSUHVTYGYRUUFUUCSYKYGYRNZUUEYEBYTUVCDEWAZYRNZYCDLZYCYQEWIZNZUUEYE
      SZYCYTLZYGUVDYRDEWBWCUVJUVHYQKLUVJUVHWDGWEDKEYQYCBWEZWFWGWHUVEUVHNZYDUUDO
      ZUVIUVLHYDUUDUVLUUNYCEWIZUUNDLZNZUVNUUNYTLZNZUUNYDLZUUNUUDLZUVLUVNUVOUVQU
      VLUVNUVOUVQUVLUVNUVOWJUVQUUNYQEWIZUVLUVNUVOUWAUVDYRUVFUVGUVNUVOUWASZSZUVD
      UVFYRUVGUWCSZUVDUVFYRUWDUVDUVFYRWJZUVNUVGUWBUWEUVNUVGUWBUWEUVOUVNUVGNZUWA
      UVDUVFYRUVOUWFUWASZSUVDUVFUVOYRUWGUVDUVOUVFYRUWGSUVDUVOUVFYRUWGDUUNYCYQEW
      KWLWMWNWOWMWPWMXAWMXBWOUVLUVOUVQUWAWDZUVNUVEUVOUWHUVHYRUVOUWHUVDDDEYQUUNW
      QWRWSWTXCXAXDUVSUVOUVNNZUVPYCKLZUVSUWIWDUVKDKEYCUUNHWEZWFWGUVOUVNXEXFUVTU
      VQUVNNZUVRUWJUVTUWLWDUVKYTKEYCUUNUWKWFWGUVQUVNXEXFXGXHUVMUUEYEYDUUDXITUHX
      JXLWSUJUUGUUCYFSUUHYEBYTDXKWGXMXNTXOUKXPXQ $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    tz6.26i.1 $e |- R We A $.
    tz6.26i.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    $( All nonempty (possibly proper) subclasses of ` A ` , which has a
       well-founded relation ` R ` , have ` R `-minimal elements.  Proposition
       6.26 of [TakeutiZaring] p. 31.  (Contributed by Scott Fenton,
       14-Apr-2011.) $)
    tz6.26i $p |- ( ( B C_ A /\ B =/= (/) ) ->
                    E. y e. B Pred ( R , B , y ) = (/) ) $=
      ( wwe cv cpred cvv wcel wral wss c0 wne wa wceq wrex tz6.26 mpanl12 ) CEH
      CEAIJKLACMDCNDOPQDEBIJORBDSFGABCDETUA $.
      $( [14-Apr-2011] $)
  $}


  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    $( The Principle of Well-Founded Induction.  Theorem 6.27 of
       [TakeutiZaring] p. 32.  This principle states that if ` B ` is a
       subclass of a well-ordered class ` A ` with the property that every
       element of ` B ` whose inital segment is included in ` A ` is itself
       equal to ` A ` .  (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfi $p |- ( ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V ) /\
                    ( B C_ A /\
                      A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) ) )
                  -> A = B ) $=
      ( cv cpred wcel wral wa wss wn cdif ssdif0 wceq wrex anbi1i cin df-pred
      c0 wwe cvv wi wne necon3bbii difss tz6.26 eldif anass ancom ccnv csn cima
      indif2 incom difeq1i 3eqtr4i eqeq1i bitr4i anbi2i 3bitri rexbii2 rexanali
      eqtri bitri sylib ex mpani syl5bi con4d imp adantrl simprl eqssd ) CEUACE
      AFGUBHACIJZDCKZCEBFZGZDKZVQDHZUCBCIZJJCDVOWACDKZVPVOWAWBVOWBWAWBLCDMZTUDZ
      VOWALZWBWCTCDNUEVOWCCKZWDWECDUFVOWFWDJZWEVOWGJWCEVQGZTOZBWCPZWEABCWCEUGWJ
      VSVTLZJZBCPWEWIWLBWCCVQWCHZWIJVQCHZWKJZWIJWNWKWIJZJWNWLJWMWOWIVQCDUHQWNWK
      WIUIWPWLWNWPWIWKJWLWKWIUJWIVSWKWIVRDMZTOVSWHWQTEUKVQULUMZWCRZWRCRZDMWHWQW
      RCDUNWHWCWRRWSWCEVQSWCWRUOVDVRWTDVRCWRRWTCEVQSCWRUOVDUPUQURVRDNUSQVEUTVAV
      BVSVTBCVCVEVFVGVHVIVJVKVLVOVPWAVMVN $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x $.  $d A y $.  $d B x $.  $d B y $.  $d R x $.  $d R y $.
    wfi.1 $e |- R We A $.
    wfi.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    $( The Principle of Well-Founded Induction.  Theorem 6.27 of
       [TakeutiZaring] p. 32.  This principle states that if ` B ` is a
       subclass of a well-ordered class ` A ` with the property that every
       element of ` B ` whose inital segment is included in ` A ` is itself
       equal to ` A ` .  (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfii $p |- ( ( B C_ A /\
                    A. y e. A ( Pred ( R , A , y ) C_ B -> y e. B ) )
                  -> A = B ) $=
      ( wwe cv cpred cvv wcel wral wa wss wi wceq pm3.2i wfi mpan ) CEHZCEAIJKL
      ACMZNDCOCEBIZJDOUCDLPBCMNCDQUAUBFGRABCDEST $.
      $( [29-Jan-2011] $)
  $}


  ${
    $d A w x y $.  $d A z $.  $d ph w x $.  $d ph z $.  $d R w x y $.
    $d R z $.  $d w y z $.
    wfisg.1 $e |- ( y e. A ->
                       ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Well-Founded Induction Schema.  If a property passes from all elements
       less than ` y ` of a well founded class ` A ` to ` y ` itself (induction
       hypothesis), then the property holds for all elements of ` A ` .
       (Contributed by Scott Fenton, 11-Feb-2011.) $)
    wfisg $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( vw cv cpred wcel wral wa wceq wss wi wsbc ax-17 elrabsf hbs1 wwe ssrab2
      cvv crab dfss3 simprbi ralimi sylbi hbral predeq3 raleqdv sbequ12 imbi12d
      hbim eleq1 chvar syl5 anc2li syl6ibr rgen wfi mpanr12 rabid2 sylib ) EFUA
      EFBIJUCKBELMZEACEUDZNZACELVEVFEOEFHIZJZVFOZVHVFKZPZHELVGACEUBVLHEVHEKZVJV
      MACVHQZMVKVMVJVNVJACDIZQZDVILZVMVNVJVOVFKZDVILVQDVIVFUEVRVPDVIVRVOEKVPACH
      VOEVMCRZSUFUGUHCIZEKZVPDEFVTJZLZAPZPVMVQVNPZPCHVMWECVSVQVNCVPCDVIVOVIKCRA
      CDTUIACHTUNUNVTVHNZWAVMWDWEVTVHEUOWFWCVQAVNWFVPDWBVIEFVTVHUJUKACHULUMUMGU
      PUQURACHVHEVSSUSUTBHEVFFVAVBACEVCVD $.
      $( [11-Feb-2011] $)
  $}


  ${
    $d A w y z $.  $d A x $.  $d ph w z $.  $d ph x $.  $d R w y z $.
    $d R x $.  $d x y $.
    wfis.1 $e |- R We A $.
    wfis.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis.3 $e |- ( y e. A ->
                    ( A. z e. Pred ( R , A , y ) [ z / y ] ph -> ph ) ) $.
    $( Well-Founded Induction Schema.  If all elements less than a given set
       ` x ` of the well founded class ` A ` have a property (induction
       hypothesis), then all elements of ` A ` have that property.
       (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfis $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfisg mp2an rspec ) ACEEFJEFBKLMNBEOACEOGHAB
      CDEFIPQR $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d R x y $.  $d R z $.
    $d y z $.
    wfis2fg.1 $e |- ( ps -> A. y ps ) $.
    wfis2fg.2 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2fg.3 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well-Founded Induction Schema, using implicit substitution.
       (Contributed by Scott Fenton, 11-Feb-2011.) $)
    wfis2fg $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( cv wsbc cpred wral wcel sbie ralbii syl5bi wfisg ) ACDEFGADEKLZEFGDKZMZ
      NBEUBNUAFOATBEUBABDEHIPQJRS $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d R x y $.  $d R z $.
    $d y z $.
    wfis2f.1 $e |- R We A $.
    wfis2f.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis2f.3 $e |- ( ps -> A. y ps ) $.
    wfis2f.4 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2f.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well Founded Induction schema, using implicit substitution.
       (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfis2f $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfis2fg mp2an rspec ) ADFFGMFGCNOPQCFRADFRHI
      ABCDEFGJKLSTUA $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d ps y $.  $d R x y $.
    $d R z $.  $d y z $.
    wfis2g.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2g.2 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well-Founded Induction Schema, using implicit substitution.
       (Contributed by Scott Fenton, 11-Feb-2011.) $)
    wfis2g $p |- ( ( R We A /\ A. x e. A Pred ( R , A , x ) e. _V )
                     -> A. y e. A ph ) $=
      ( ax-17 wfis2fg ) ABCDEFGBDJHIK $.
      $( [11-Feb-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d ph x $.  $d ph z $.  $d ps y $.  $d R x y $.
    $d R z $.  $d y z $.
    wfis2.1 $e |- R We A $.
    wfis2.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis2.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis2.4 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.
    $( Well Founded Induction schema, using implicit substitution.
       (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfis2 $p |- ( y e. A -> ph ) $=
      ( wwe cv cpred cvv wcel wral wfis2g mp2an rspec ) ADFFGLFGCMNOPCFQADFQHIA
      BCDEFGJKRST $.
      $( [29-Jan-2011] $)
  $}

  ${
    $d A x y $.  $d A z $.  $d B y $.  $d ch y $.  $d ph x $.  $d ph z $.
    $d ps y $.  $d R x y $.  $d R z $.  $d y z $.
    wfis3.1 $e |- R We A $.
    wfis3.2 $e |- A. x e. A Pred ( R , A , x ) e. _V $.
    wfis3.3 $e |- ( y = z -> ( ph <-> ps ) ) $.
    wfis3.4 $e |- ( y = B -> ( ph <-> ch ) ) $.
    wfis3.5 $e |- ( y e. A -> ( A. z e. Pred ( R , A , y ) ps -> ph ) ) $.


    $( Well Founded Induction schema, using implicit substitution.
       (Contributed by Scott Fenton, 29-Jan-2011.) $)
    wfis3 $p |- ( B e. A -> ch ) $=
      ( wfis2 vtoclga ) ACEHGMABDEFGIJKLNOP $.
      $( [29-Jan-2011] $)
  $}

