$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Birthday Function
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d f x y $.
    $( The birthday function maps the surreals onto the ordinals.  Alling's
       axiom (B). $)
    axbday $p |- bday : No -onto-> On $=
      ( vf vx vy csur con0 cbday wfo wfn crn wceq df-fo cv cdm cvv wcel wral
      dmexg rgen df-bday mptfn mpbi wrex cab rnmpt c1o csn cxp dmeq eqeq2d
      rcla4ev noxp1o c0 wne 1on elisseti snnz dmxp ax-mp eqcomi sylancl c2o
      cpr wf wi elno weq wa eleq1 biimpcd eqtr3 eqcom sylanb syl5 exp3a fdm
      r19.23aiv sylbi impbii abbi2i eqtr4i mpbir2an ) DEFGFDHZFIZEJDEFKALZMZNOZ
      ADPWBWFADWDDQRADWEFASZTUAWCBLZWEJZADUBZBUCEABDWEFWGUDWJBEWHEOZWJWHUEUFZUG
      ZDOWHWMMZJZWJWKWIWOAWMDWDWMJWEWNWHWDWMUHUIUJWHUKWNWHWLULUMWNWHJUEUEEUNUOU
      PWHWLUQURUSUTWIWKADWDDOCLZUEVAVBZWDVCZCEUBWIWKVDZCWDVEWRWSCEWPEOZWEWPJZWS
      WRWTXAWIWKWTCBVFZWKXAWIVGXBWTWKWPWHEVHVIWPWEJWIXBXAWPWHWEVJWEWPVKVLVMVNWP
      WQWDVOVMVPVQVPVRVSVTWA $.
      $( [11-Jun-2011] $)

  $}

  $( The birthday function is a function. $)
  bdayfun $p |- Fun bday $=
    ( csur con0 cbday wfo wfun axbday fofun ax-mp ) ABCDCEFABCGH $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function's range is ` On ` $)
  bdayrn $p |- ran bday = On $=
    ( csur con0 cbday wfo crn wceq axbday forn ax-mp ) ABCDCEBFGABCHI $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function's domain is ` No ` $)
  bdaydm $p |- dom bday = No $=
    ( csur con0 cbday wf cdm wceq wfo axbday fof ax-mp fdm ) ABCDZCEAFABCGLHABC
    IJABCKJ $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The birthday function is a function over ` No ` $)
  bdayfn $p |- bday Fn No $=
    ( cbday csur wfn wfun cdm wceq df-fn bdayfun bdaydm mpbir2an ) ABCADAEBFABG
    HIJ $.
    $( [10-Jul-2011] $) $( [30-Jun-2011] $)

  $( The value of the birthday function is always an ordinal. $)
  bdayelon $p |- ( bday ` A ) e. On $=
    ( cbday cdm wcel cfv con0 wfun bdayfun wa crn fvelrn bdayrn syl6eleq mpan
    wn c0 ndmfv 0elon syl6eqel pm2.61i ) ABCDZABEZFDZBGZUAUCHUDUAIUBBJFABKLMNUA
    OUBPFABQRST $.
    $( [10-Jul-2011] $) $( [14-Jun-2011] $)

  $( The surreal numbers are a proper class. $)
  noprc $p |- -. No e. _V $=
    ( csur cvv wcel con0 onprc cbday wfo axbday fornex mpi mto ) ABCZDBCZELADFG
    MHADBFIJK $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

