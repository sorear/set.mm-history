$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Ordering
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.
    $( Lemma for ~ axsltso .  The sign expansion relationship totally orders
       the surreal signs. $)
    axsltsolem1 $p |- { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                      Or ( { 1o , 2o } u. { (/) } ) $=
      ( vx vy vz c1o c2o c0 cop wbr wcel wceq wa w3o eqtr2 mto 0ex expcom 3jaod
      brtp ex ad2ant2lr ctp wor cpr csn cun cv wn wne 1n0 df-ne mpbi con0 0elon
      wb 1on csuc df-2o df-1o eqeq12i suc11 syl5bb mp2an nemtbir ancoms nsuceq0
      eqeq1i 3pm3.2ni vex 2on elexi mtbir a1i w3a pm2.21i ad2ant2rl 3mix2 3jaoi
      wi imp anbi12i 3imtr4i weq eqtr3 3mix2d 3mix1d 3mix1 3mix3 eltp 3orbi123i
      3mix3d biid itlso df-tp soeq2 ax-mp ) DEFUAZDFGDEGFEGUAZUBZDEUCFUDUEZWQUB
      ZABCWPWQAUFZXAWQHZUGXAWPIZXBXADJZXAFJZKZXDXAEJZKZXEXGKZLXFXHXIXFDFJZDFUHX
      JUGUIDFUJUKZXADFMNXHEDJZXLDFUIDULIZFULIZXLXJUNUOUMXLDUPZFUPZJXMXNKXJEXODX
      PUQURUSDFUTVAVBVCZXGXDXLXAEDMVDNXIEFJZXRXOFDVEEXOFUQVFVCZXGXEXRXAEFMVDNVG
      DFDEFEXAXAAVHZXTOEULVIVJZYARVKVLXABUFZWQHZYBCUFZWQHZKZXAYDWQHZVRXCYBWPIZY
      DWPIVMXDYBFJZKZXDYBEJZKZXEYKKZLZYBDJZYDFJZKZYOYDEJZKZYIYRKZLZKXDYPKZXDYRK
      ZXEYRKZLZYFYGYNUUAUUEYJUUAUUEVRYLYMYJYQUUEYSYTYQYJUUEYOYIUUEYPXDYOYIKZUUE
      UUFXJXKYBDFMNVNZVOPYSYJUUEYOYIUUEYRXDUUGVOPYJYTUUEXDYRUUEYIYIUUCUUBUUDVPV
      OSQYLYQUUEYSYTYLYQUUEYKYOUUEXDYPYKYOKZUUEUUHXLXQYBEDMNVNZTSYLYSUUEYKYOUUE
      XDYRUUITSYLYTUUEYKYIUUEXDYRYKYIKZUUEUUJXRXSYBEFMNVNZTSQYMYQUUEYSYTYMYQUUE
      YKYOUUEXEYPUUITSYMYSUUEYKYOUUEXEYRUUITSYMYTUUEYKYIUUEXEYRUUKTSQVQVSYCYNYE
      UUADFDEFEXAYBXTBVHZOYAYARZDFDEFEYBYDUULCVHZOYAYARVTDFDEFEXAYDXTUUNOYAYARW
      AVLXDXGXELZYOYKYILZKYNABWBZYOXEKZYOXGKZYIXGKZLZLZXCYHKYCUUQYBXAWQHZLUUOUU
      PUVBXDUUPUVBVRXGXEXDYOUVBYKYIXDYOUVBXDYOKUUQYNUVAXAYBDWCWDSXDYKUVBYLYNUUQ
      UVAYLYJYMVPWESXDYIUVBYJYNUUQUVAYJYLYMWFWESQXGYOUVBYKYIYOXGUVBUUSUVAYNUUQU
      USUURUUTVPWJPXGYKUVBXGYKKUUQYNUVAXAYBEWCWDSYIXGUVBUUTUVAYNUUQUUTUURUUSWGW
      JPQXEYOUVBYKYIYOXEUVBUURUVAYNUUQUURUUSUUTWFWJPXEYKUVBYMYNUUQUVAYMYJYLWGWE
      SXEYIUVBXEYIKUUQYNUVAXAYBFWCWDSQVQVSXCUUOYHUUPXADEFXTWHYBDEFUULWHVTYCYNUU
      QUUQUVCUVAUUMUUQWKDFDEFEYBXAUULXTOYAYARWIWAWLWPWSJWRWTUNDEFWMWPWSWQWNWOUK
      $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d f g x y $.
    $( Surreal less than totally orders the surreals.  Alling's axiom (O).
       (Contributed by Scott Fenton, 9-Jun-2011.) $)
    axsltso $p |- <s Or No $=
      ( vx vy vf vg c1o c2o cpr c0 cop ctp cslt csur axsltsolem1 df-slt nosgnn0
      df-no soseq ) ABEFGEHIEFIHFIJKCDLMCAPABCDNOQ $.
      $( [9-Jun-2011] $)
  $}

  $( Surreal less than is irreflexive.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  sltirr $p |- ( A e. No -> -. A <s A ) $=
    ( csur cslt wor wcel wbr wn axsltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than is transitive.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  slttr $p |- ( ( A e. No /\ B e. No /\ C e. No ) ->
                ( ( A <s B /\ B <s C ) -> A <s C ) ) $=
    ( csur cslt wor wcel w3a wbr wa wi axsltso sotr mpan ) DEFADGBDGCDGHABEIBCE
    IJACEIKLDABCEMN $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than is asymmetric.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  sltasym $p |- ( ( A e. No /\ B e. No ) -> ( A <s B -> -. B <s A ) ) $=
    ( csur wcel wa cslt wbr wn sltirr ad2antrr wi slttr 3anidm13 expdimp mtod
    ex ) ACDZBCDZEZABFGZBAFGZHSTEUAAAFGZQUBHRTAIJSTUAUBQRTUAEUBKABALMNOP $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than obeys trichotomy.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  slttri $p |- ( ( A e. No /\ B e. No ) -> ( A <s B \/ A = B \/ B <s A ) ) $=
    ( csur cslt wor wcel wa wbr wceq w3o axsltso solin mpan ) CDEACFBCFGABDHABI
    BADHJKCABDLM $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Trichotomy law for surreal less than.  (Contributed by Scott Fenton,
     22-Apr-2012.) $)
  slttrieq2 $p |- ( ( A e. No /\ B e. No ) ->
                    ( A = B <-> ( -. A <s B /\ -. B <s A ) ) ) $=
    ( csur cslt wor wcel wa wceq wbr wn wb axsltso sotrieq2 mpan ) CDEACFBCFGAB
    HABDIJBADIJGKLCABDMN $.
    $( [12-May-2012] $) $( [22-Apr-2012] $)

