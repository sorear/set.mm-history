$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Surreal Numbers: Ordering
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z $.
    $( Lemma for ~ axsltso .  The sign expansion relationship totally orders
       the surreal signs. $)
    axsltsolem1 $p |- { <. 1o , (/) >. , <. 1o , 2o >. , <. (/) , 2o >. }
                      Or ( { 1o , 2o } u. { (/) } ) $=
      ( vx vy vz c1o c2o c0 ctp cop wor cpr csn cun cv wbr wn wcel wceq wa w3o
      wne 1n0 df-ne mpbi eqtr2 mto con0 wb 1on 0elon csuc df-2o df-1o eqeq12i
      suc11 syl5bb mp2an nemtbir ancoms nsuceq0 eqeq1i 3pm3.2ni visset 0ex 2on
      elisseti brtp mtbir a1i wi w3a pm2.21i ad2ant2rl expcom 3mix2 ex 3jaod
      ad2ant2lr 3jaoi imp anbi12i 3imtr4i weq eqtr3 3mix2d 3mix1d 3mix1 3mix3d
      3mix3 eltp biid 3orbi123i itlso df-tp soeq2 ax-mp ) DEFGZDFHDEHFEHGZIZDEJ
      FKLZWQIZABCWPWQAMZXAWQNZOXAWPPZXBXADQZXAFQZRZXDXAEQZRZXEXGRZSXFXHXIXFDFQZ
      DFTXJOUADFUBUCZXADFUDUEXHEDQZXLDFUADUFPZFUFPZXLXJUGUHUIXLDUJZFUJZQXMXNRXJ
      EXODXPUKULUMDFUNUOUPUQZXGXDXLXAEDUDURUEXIEFQZXRXOFDUSEXOFUKUTUQZXGXEXRXAE
      FUDURUEVADFDEFEXAXAAVBZXTVCEUFVDVEZYAVFVGVHXABMZWQNZYBCMZWQNZRZXAYDWQNZVI
      XCYBWPPZYDWPPVJXDYBFQZRZXDYBEQZRZXEYKRZSZYBDQZYDFQZRZYOYDEQZRZYIYRRZSZRXD
      YPRZXDYRRZXEYRRZSZYFYGYNUUAUUEYJUUAUUEVIYLYMYJYQUUEYSYTYQYJUUEYOYIUUEYPXD
      YOYIRZUUEUUFXJXKYBDFUDUEVKZVLVMYSYJUUEYOYIUUEYRXDUUGVLVMYJYTUUEXDYRUUEYIY
      IUUCUUBUUDVNVLVOVPYLYQUUEYSYTYLYQUUEYKYOUUEXDYPYKYORZUUEUUHXLXQYBEDUDUEVK
      ZVQVOYLYSUUEYKYOUUEXDYRUUIVQVOYLYTUUEYKYIUUEXDYRYKYIRZUUEUUJXRXSYBEFUDUEV
      KZVQVOVPYMYQUUEYSYTYMYQUUEYKYOUUEXEYPUUIVQVOYMYSUUEYKYOUUEXEYRUUIVQVOYMYT
      UUEYKYIUUEXEYRUUKVQVOVPVRVSYCYNYEUUADFDEFEXAYBXTBVBZVCYAYAVFZDFDEFEYBYDUU
      LCVBZVCYAYAVFVTDFDEFEXAYDXTUUNVCYAYAVFWAVHXDXGXESZYOYKYISZRYNABWBZYOXERZY
      OXGRZYIXGRZSZSZXCYHRYCUUQYBXAWQNZSUUOUUPUVBXDUUPUVBVIXGXEXDYOUVBYKYIXDYOU
      VBXDYORUUQYNUVAXAYBDWCWDVOXDYKUVBYLYNUUQUVAYLYJYMVNWEVOXDYIUVBYJYNUUQUVAY
      JYLYMWFWEVOVPXGYOUVBYKYIYOXGUVBUUSUVAYNUUQUUSUURUUTVNWGVMXGYKUVBXGYKRUUQY
      NUVAXAYBEWCWDVOYIXGUVBUUTUVAYNUUQUUTUURUUSWHWGVMVPXEYOUVBYKYIYOXEUVBUURUV
      AYNUUQUURUUSUUTWFWGVMXEYKUVBYMYNUUQUVAYMYJYLWHWEVOXEYIUVBXEYIRUUQYNUVAXAY
      BFWCWDVOVPVRVSXCUUOYHUUPXADEFXTWIYBDEFUULWIVTYCYNUUQUUQUVCUVAUUMUUQWJDFDE
      FEYBXAUULXTVCYAYAVFWKWAWLWPWSQWRWTUGDEFWMWPWSWQWNWOUC $.
      $( [8-Jun-2011] $)
  $}

  ${
    $d f g x y $.
    $( Surreal less than totally orders the surreals.  Alling's axiom (O).
       (Contributed by Scott Fenton, 9-Jun-2011.) $)
    axsltso $p |- <s Or No $=
      ( vx vy vf vg c1o c2o cpr c0 cop ctp cslt csur axsltsolem1 df-no df-slt
      nosgnn0 soseq ) ABEFGEHIEFIHFIJKCDLMCANABCDOPQ $.
      $( [9-Jun-2011] $)
  $}

  $( Surreal less than is irreflexive.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  sltirr $p |- ( A e. No -> -. A <s A ) $=
    ( csur cslt wor wcel wbr wn axsltso sonr mpan ) BCDABEAACFGHBACIJ $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than is transitive.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  slttr $p |- ( ( A e. No /\ B e. No /\ C e. No ) ->
                ( ( A <s B /\ B <s C ) -> A <s C ) ) $=
    ( csur cslt wor wcel w3a wbr wa wi axsltso sotr mpan ) DEFADGBDGCDGHABEIBCE
    IJACEIKLDABCEMN $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than is asymmetric.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  sltasym $p |- ( ( A e. No /\ B e. No ) -> ( A <s B -> -. B <s A ) ) $=
    ( csur wcel wa cslt wbr wn sltirr ad2antrr wi slttr 3anidm13 expdimp mtod
    ex ) ACDZBCDZEZABFGZBAFGZHSTEUAAAFGZQUBHRTAIJSTUAUBQRTUAEUBKABALMNOP $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Surreal less than obeys trichotomy.  (Contributed by Scott Fenton,
     16-Jun-2011.) $)
  slttri $p |- ( ( A e. No /\ B e. No ) -> ( A <s B \/ A = B \/ B <s A ) ) $=
    ( csur cslt wor wcel wa wbr wceq w3o axsltso solin mpan ) CDEACFBCFGABDHABI
    BADHJKCABDLM $.
    $( [10-Jul-2011] $) $( [16-Jun-2011] $)

  $( Trichotomy law for surreal less than.  (Contributed by Scott Fenton,
     22-Apr-2012.) $)
  slttrieq2 $p |- ( ( A e. No /\ B e. No ) ->
                    ( A = B <-> ( -. A <s B /\ -. B <s A ) ) ) $=
    ( csur cslt wor wcel wa wceq wbr wn wb axsltso sotrieq2 mpan ) CDEACFBCFGAB
    HABDIJBADIJGKLCABDMN $.
    $( [12-May-2012] $) $( [22-Apr-2012] $)

