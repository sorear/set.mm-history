$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Greatest common divisor and divisibility
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( ` gcd ` of the absolute value of the first operator.  (Contributed by
     Scott Fenton, 02-Apr-2014.) $)
  gcdabs1 $p |- ( ( N e. ZZ /\ M e. ZZ ) ->
    ( ( abs ` N ) gcd M ) = ( N gcd M ) ) $=
    ( cz wcel wa cabs cfv wceq cneg wo co cr zre absor syl adantr wi opreq1 a1i
    cgcd neggcd eqeq1d syl5ibrcom jaod mpd ) BCDZACDZEZBFGZBHZUIBIZHZJZUIATKZBA
    TKZHZUFUMUGUFBLDUMBMBNOPUHUJUPULUJUPQUHUIBATRSUHUPULUKATKZUOHBAUAULUNUQUOUI
    UKATRUBUCUDUE $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( ` gcd ` of the absolute value of the second operator.  (Contributed by
     Scott Fenton, 02-Apr-2014.) $)
  gcdabs2 $p |- ( ( N e. ZZ /\ M e. ZZ ) ->
    ( N gcd ( abs ` M ) ) = ( N gcd M ) ) $=
    ( cz wcel wa cabs cfv cgcd wceq gcdabs1 ancoms cn0 nn0abscl nn0z syl gcdcom
    co sylan2 3eqtr4d ) BCDZACDZEAFGZBHQZABHQZBUBHQZBAHQUATUCUDIBAJKUATUBCDZUEU
    CIUAUBLDUFAMUBNOBUBPRBAPS $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( Biconditional form of ~ dvdsgcd .  (Contributed by Scott Fenton,
     02-Apr-2014.) $)
  dvdsgcdb $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. ZZ ) ->
    ( ( K || M /\ K || N ) <-> K || ( M gcd N ) ) ) $=
    ( cz wcel w3a cdivides wbr wa cgcd co dvdsgcd gcddvds simpld 3adant1 wi cn0
    simp1 dvdstr mpan2d gcdcl nn0z simp2 syl111anc simprd syld3an2 jcad impbid
    syl ) ADEZBDEZCDEZFZABGHZACGHZIABCJKZGHZABCLUMUQUNUOUMUQUPBGHZUNUKULURUJUKU
    LIZURUPCGHZBCMZNOUMUJUPDEZUKUQURIUNPUJUKULRUKULVBUJUSUPQEVBBCUAUPUBUIOZUJUK
    ULUCAUPBSUDTUMUQUTUOUKULUTUJUSURUTVAUEOUJVBUKULUQUTIUOPVCAUPCSUFTUGUH $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  ${
    $d N x $.  $d M x $.  $d P x $.
    $( Associative law for ` gcd ` operator.  (Contributed by Scott Fenton,
       02-Apr-2014.) $)
    gcdass $p |- ( ( N e. ZZ /\ M e. ZZ /\ P e. ZZ ) ->
    ( ( N gcd M ) gcd P ) = ( N gcd ( M gcd P ) ) ) $=
      ( vx cz wcel cc0 wceq wa cdivides wbr crab cr clt csup cif cgcd co anass
      wb w3a cv a1i rabbiia supeq1i ifbieq2i cn0 gcdcl 3adant3 syl simp3 gcdval
      nn0z syl11anc gcdeq0 anbi1d bicomd simpr simpl1 simpl2 dvdsgcdb syl111anc
      rabbidva supeq1d ifbieq2d eqtr4d simp1 3adant1 anbi2d simpl3 3eqtr4a ) CE
      FZBEFZAEFZUAZCGHZBGHZIZAGHZIZGDUBZCJKZWABJKZIZWAAJKZIZDELZMNOZPZVPVQVSIZI
      ZGWBWCWEIZIZDELZMNOZPZCBQRZAQRZCBAQRZQRZVTWKWHWOGVPVQVSSMWGWNNWFWMDEWFWMT
      WAEFZWBWCWESUCUDUEUFVOWRWQGHZVSIZGWAWQJKZWEIZDELZMNOZPZWIVOWQEFZVNWRXHHVO
      WQUGFZXIVLVMXJVNCBUHUIWQUMUJVLVMVNUKDWQAULUNVOVTXCWHXGGVOXCVTVOXBVRVSVLVM
      XBVRTVNCBUOUIUPUQVOMWGXFNVOWFXEDEVOXAIZWDXDWEXKXAVLVMWDXDTVOXAURZVLVMVNXA
      USVLVMVNXAUTZWACBVAVBUPVCVDVEVFVOWTVPWSGHZIZGWBWAWSJKZIZDELZMNOZPZWPVOVLW
      SEFZWTXTHVLVMVNVGVOWSUGFZYAVMVNYBVLBAUHVHWSUMUJDCWSULUNVOWKXOWOXSGVOXOWKV
      OXNWJVPVMVNXNWJTVLBAUOVHVIUQVOMWNXRNVOWMXQDEXKWLXPWBXKXAVMVNWLXPTXLXMVLVM
      VNXAVJWABAVAVBVIVCVDVEVFVK $.
      $( [19-Apr-2014] $) $( [2-Apr-2014] $)
  $}

  $( Reverse distribution law for the ` gcd ` operator.  (Contributed by Scott
     Fenton, 02-Apr-2014.) $)
  mulgcdr $p |- ( ( A e. ZZ /\ B e. ZZ /\ C e. NN0 ) ->
    ( ( A x. C ) gcd ( B x. C ) ) = ( ( A gcd B ) x. C ) ) $=
    ( cz wcel cn0 w3a cmul co cgcd wceq mulgcd 3coml cc 3ad2ant1 nn0cn 3ad2ant3
    zcn mulcom syl11anc 3ad2ant2 opreq12d gcdcl 3adant3 syl 3eqtr4d ) ADEZBDEZC
    FEZGZCAHIZCBHIZJIZCABJIZHIZACHIZBCHIZJIUNCHIZUIUGUHUMUOKCABLMUJUPUKUQULJUJA
    NEZCNEZUPUKKUGUHUSUIAROUIUGUTUHCPQZACSTUJBNEZUTUQULKUHUGVBUIBRUAVABCSTUBUJU
    NNEZUTURUOKUJUNFEZVCUGUHVDUIABUCUDUNPUEVAUNCSTUF $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( If three numbers are coprime, and the square of one is the product of the
     other two, then there is a formula for the other two in terms of ` gcd `
     and square.  (Contributed by Scott Fenton, 02-Apr-2014.) $)
  coprimeprodsq $p |- ( ( ( A e. NN0 /\ B e. ZZ /\ C e. NN0 ) /\
    ( ( A gcd B ) gcd C ) = 1 ) -> ( ( C ^ 2 ) = ( A x. B ) ->
      A = ( ( A gcd C ) ^ 2 ) ) ) $=
    ( cn0 wcel cz cgcd co c1 wceq cmul cc 3ad2ant1 nn0cn syl syl11anc syl111anc
    nn0z mulgcd 3eqtr3d w3a c2 cexp gcdcl syl2an 3adant2 sqval simp13 mulcom wa
    simpl3 eqeq1d biimp3a opreq12d simp11 simp12 opreq2d mulgcdr ancoms 3adant1
    sylan cabs 3ad2ant3 gcdid opreq1d simp2 gcdabs1 eqtrd gcdass gcdcom 3eqtr4d
    cfv biimpar 3adant3 mulid1 3eqtrrd 3expia ) ADEZBFEZCDEZUAZABGHCGHZIJZCUBUC
    HZABKHZJZAACGHZUBUCHZJWAWCWFUAZWHWGWGKHZAWGCBGHZGHZKHZAWIWGLEZWHWJJWIWGDEZW
    NWAWCWOWFVRVTWOVSVRAFEZCFEZWOVTARZCRZACUDUEUFMZWGNOWGUGOWIAWGKHZCWGKHZGHZXA
    AWKKHZGHZWJWMWIXBXDXAGWICAKHZCCKHZGHZACKHZWEGHZXBXDWIXFXIXGWEGWICLEZALEZXFX
    IJWIVTXKVRVSVTWCWFUHZCNZOWAWCXLWFVRVSXLVTANMMZCAUIPWAWCWFXGWEJWAWCUJZWDXGWE
    XPXKWDXGJXPVTXKVRVSVTWCUKXNOCUGOULUMUNWIVTWPWQXHXBJXMWIVRWPVRVSVTWCWFUOZWRO
    ZWIVTWQXMWSOZCACSQWIVRWQVSXJXDJXQXSVRVSVTWCWFUPACBSQTUQWIWPWQWOXCWJJXRXSWTA
    CWGURQWIVRWGFEZWKFEZXEWMJXQWIWOXTWTWGROWIWKDEZYAWAWCYBWFVSVTYBVRVTVSYBVTWQV
    SYBWSCBUDVAUSUTZMWKRZOAWGWKSQTWIWMAIKHZAWAWCWMYEJWFXPWLIAKWAWLIJWCWAWLWBIWA
    ACWKGHZGHZABCGHZGHZWLWBWAYFYHAGWACCGHZBGHZWKYFYHWAYKCVBVLZBGHZWKWAYJYLBGWAW
    QYJYLJVTVRWQVSWSVCZCVDOVEWAWQVSYMWKJYNVRVSVTVFZBCVGPVHWAWQWQVSYKYFJYNYNYOBC
    CVIQWAWQVSWKYHJYNYOCBVJPTUQWAWPWQYAWLYGJVRVSWPVTWRMZYNWAYBYAYCYDOWKCAVIQWAW
    PVSWQWBYIJYPYOYNCBAVIQVKULVMUQVNWIXLYEAJXOAVOOVHVPVQ $.
    $( [19-Apr-2014] $) $( [2-Apr-2014] $)

  $( If three numbers are coprime, and the square of one is the product of the
     other two, then there is a formula for the other two in terms of ` gcd `
     and square.  (Contributed by Scott Fenton, 17-Apr-2014.) $)
  coprimeprodsq2 $p |- ( ( ( A e. ZZ /\ B e. NN0 /\ C e. NN0 ) /\
    ( ( A gcd B ) gcd C ) = 1 ) -> ( ( C ^ 2 ) = ( A x. B ) ->
      B = ( ( B gcd C ) ^ 2 ) ) ) $=
    ( cz wcel cn0 w3a cgcd co c1 wceq wa c2 cexp cmul zcn nn0cn mulcom 3adant3
    cc syl2an adantr eqeq2d wi simpl2 simpl1 simpl3 gcdcom eqeq1d sylan2 biimpa
    wb nn0z opreq1d coprimeprodsq syl31anc sylbid ) ADEZBFEZCFEZGZABHIZCHIZJKZL
    ZCMNIZABOIZKVFBAOIZKZBBCHIMNIKZVEVGVHVFVAVGVHKZVDURUSVKUTURATEBTEVKUSAPBQAB
    RUASUBUCVEUSURUTBAHIZCHIZJKZVIVJUDURUSUTVDUEURUSUTVDUFURUSUTVDUGVAVDVNURUSV
    DVNULZUTUSURBDEZVOBUMURVPLZVCVMJVQVBVLCHABUHUNUIUJSUKBACUOUPUQ $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  ${
    $d j k $.  $d j m $.  $d j n $.  $d j x $.  $d j y $.  $d k m $.  $d k y $.
    $d m n $.  $d m x $.  $d m y $.  $d N j $.  $d N k $.  $d N n $.  $d n x $.
    $d n y $.  $d k x $.
    $( Lemma for ~ odd2np1 .  (Contributed by Scott Fenton, 03-Apr-2014.) $)
    odd2np1lem $p |- ( N e. NN0 -> (
      E. n e. ZZ ( ( 2 x. n ) + 1 ) = N \/ E. k e. ZZ ( k x. 2 ) = N ) ) $=
      ( vy c2 cv cmul co c1 caddc wceq cz wrex wo cc0 eqeq2 rexbidv eqeq1d wcel
      2cn vj vx orbi12d weq opreq2 opreq1d cbvrexv syl6bb opreq1 mul02i rcla4ev
      vm 0z mp2an olci cn0 orcom wa cc zcn mulcom sylancl adantl wi eqid eqeq2d
      mpan2 syl5ibcom sylbid rexlimdva peano2z mulid2i a1i opreq12d df-2 syl6eq
      opreq2i ax-1cn adddir mp3an23 mpan addass 3eqtr4d syl11anc orim12d syl5bi
      mulcl syl nn0ind ) EBFZGHZIJHZUAFZKZBLMZAFZEGHZWMKZALMZNWLOKZBLMZWQOKZALM
      ZNEUBFZGHZIJHZULFZKZUBLMZDFZEGHZXGKZDLMZNZWLXGIJHZKZBLMZWQXOKZALMZNZWLCKZ
      BLMZWQCKZALMZNUAULCWMOKZWOXAWSXCYEWNWTBLWMOWLPQYEWRXBALWMOWQPQUCUAULUDZWO
      XIWSXMYFWOWLXGKZBLMXIYFWNYGBLWMXGWLPQYGXHBUBLBUBUDZWLXFXGYHWKXEIJWJXDEGUE
      UFRUGUHYFWSWQXGKZALMXMYFWRYIALWMXGWQPQYIXLADLADUDWQXKXGWPXJEGUIRUGUHUCWMX
      OKZWOXQWSXSYJWNXPBLWMXOWLPQYJWRXRALWMXOWQPQUCWMCKZWOYBWSYDYKWNYABLWMCWLPQ
      YKWRYCALWMCWQPQUCXCXAOLSOEGHZOKZXCUMETUJXBYMAOLWPOKWQYLOWPOEGUIRUKUNUOXNX
      MXINXGUPSZXTXIXMUQYNXMXQXIXSYNXLXQDLYNXJLSZURZXLEXJGHZXGKZXQYPXKYQXGYOXKY
      QKZYNYOXJUSSEUSSZYSXJUTTXJEVAVBVCRYOYRXQVDYNYOWLYQIJHZKZBLMZYRXQYOUUAUUAK
      ZUUCUUAVEUUBUUDBXJLBDUDZWLUUAUUAUUEWKYQIJWJXJEGUEUFRUKVGYRUUBXPBLYRUUAXOW
      LYQXGIJUIVFQVHVCVIVJYNXHXSUBLYNXDLSZURZWQXFIJHZKZALMZXHXSUUGXDIJHZLSZUUKE
      GHZUUHKZUUJUUFUULYNXDVKVCUUFUUNYNUUFXDUSSZUUNXDUTUUOXDEGHZIEGHZJHZXEIIJHZ
      JHZUUMUUHUUOUURXEEJHUUTUUOUUPXEUUQEJUUOYTUUPXEKTXDEVAVGUUQEKUUOETVLVMVNEU
      USXEJVOVQVPUUOIUSSZYTUUMUURKVRTXDIEVSVTUUOXEUSSZUUHUUTKZYTUUOUVBTEXDWGWAU
      VBUVAUVAUVCVRVRXEIIWBVTWHWCWHVCUUIUUNAUUKLWPUUKKWQUUMUUHWPUUKEGUIRUKWDXHU
      UIXRALXHUUHXOWQXFXGIJUIVFQVHVJWEWFWI $.
      $( [19-Apr-2014] $) $( [3-Apr-2014] $)
  $}

  ${
    $d N n k x y $.
    $( An integer is odd iff it is one plus twice another integer.
       (Contributed by Scott Fenton, 03-Apr-2014.) $)
    odd2np1 $p |- ( N e. ZZ ->
     ( -. 2 || N <-> E. n e. ZZ ( ( 2 x. n ) + 1 ) = N ) ) $=
      ( vk cz wcel c2 cmul co wceq wrex c1 caddc wb wa cneg syl 2cn cmin ax-1cn
      cc vx vy cdivides wbr wn cv 2z divides notbid wo cr cn0 elznn0 odd2np1lem
      mpan adantl peano2z znegcl ad2antlr mulcl peano2cn simpl negcon2 syl11anc
      zcn recnd eqcom sylancr negsubdi sylancl mulcli addsubass mp3an23 mulid1i
      opreq1i 1p1e2 subaddrii eqtri opreq2i syl6req adddi mp3an13 eqtr4d negeqd
      opreq1d mulneg2 3eqtr4d eqeq1d syl5bb bitrd biimpa opreq2 rcla4ev ex recn
      rexlimdva syl2anc mulneg1 syl6rbbr opreq1 orim12d imp jaodan sylbi halfnz
      syl5 cdiv reeanv eqtr3 mulcom eqeq2d subadd mp3an3 subcl cc0 wne 2ne0 w3a
      pm3.2i divmul ancoms subdi mp3an1 syl2an wi zsubcl eleq1 syl5ibcom sylbid
      sylbird rexlimivv sylbir mto jctir xor3 xor2 bicom 3bitr3i sylib ) BDEZFB
      UCUDZUECUFZFGHZBIZCDJZUEZFAUFZGHZKLHZBIZADJZYTUUAUUEFDEYTUUAUUEMUGCFBUHUO
      UIYTUUKUUEUJZUUKUUENZUEZNZUUFUUKMZYTUULUUNYTBUKEZBULEZBOZULEZUJNUULBUMUUQ
      UURUULUUTUURUULUUQCABUNUPUUQUUTUULUUTFUAUFZGHZKLHZUUSIZUADJZUBUFZFGHZUUSI
      ZUBDJZUJUUQUULUBUAUUSUNUUQUVEUUKUVIUUEUUQUVDUUKUADUUQUVADEZNZUVDUUKUVKUVD
      NUVAKLHZOZDEZFUVMGHZKLHZBIZUUKUVJUVNUUQUVDUVJUVLDEZUVNUVAUQZUVLURPUSUVKUV
      DUVQUVKUVDBUVCOZIZUVQUVKUVCTEZBTEZUVDUWAMUVJUWBUUQUVJUVATEZUWBUVAVEZUWDUV
      BTEZUWBFTEZUWDUWFQFUVAUTZUOUVBVAPPUPUVKBUUQUVJVBVFUVCBVCVDUWAUVTBIUVKUVQB
      UVTVGUVKUVTUVPBUVJUVTUVPIUUQUVJFUVLGHZKRHZOZUWIOZKLHZUVTUVPUVJUWITEZKTEZU
      WKUWMIUVJUWGUVLTEZUWNQUVJUVRUWPUVSUVLVEPZFUVLUTVHSUWIKVIVJUVJUVCUWJUVJUVC
      UVBFKGHZLHZKRHZUWJUVJUWTUVBUWRKRHZLHZUVCUVJUWFUWTUXBIZUVJUWGUWDUWFQUWEUWH
      VHUWFUWRTEUWOUXCFKQSVKSUVBUWRKVLVMPUXAKUVBLUXAFKRHKUWRFKRFQVNVOFKKQSSVPVQ
      VRVSVTUVJUWIUWSKRUVJUWDUWIUWSIZUWEUWGUWDUWOUXDQSFUVAKWAWBPWEWCWDUVJUVOUWL
      KLUVJUWGUWPUVOUWLIQUWQFUVLWFVHWEWGUPWHWIWJWKUUJUVQAUVMDUUGUVMIZUUIUVPBUXE
      UUHUVOKLUUGUVMFGWLWEWHWMVDWNWPUUQUVHUUEUBDUUQUVFDEZNZUVHUUEUXGUVHNUVFOZDE
      ZUXHFGHZBIZUUEUXFUXIUUQUVHUVFURUSUXGUVHUXKUXGUVHBUVGOZIZUXKUXFUVGTEZUWCUV
      HUXMMUUQUXFUVFTEZUWGUXNUVFVEZQUVFFUTVJBWOUVGBVCWQUXGUXKUXLBIUXMUXGUXJUXLB
      UXFUXJUXLIZUUQUXFUXOUWGUXQUXPQUVFFWRVJUPWHBUXLVGWSWJWKUUDUXKCUXHDUUBUXHIU
      UCUXJBUUBUXHFGWTWHWMVDWNWPXAXFXBXCXDUUMKFXGHZDEZXEUUMUUJUUDNZCDJADJUXSUUJ
      UUDACDDXHUXTUXSACDDUXTUUIUUCIZUUGDEZUUBDEZNZUXSUUIUUCBXIUYDUYAUUIFUUBGHZI
      ZUXSUYCUYAUYFMUYBUYCUUCUYEUUIUYCUUBTEZUWGUUCUYEIUUBVEZQUUBFXJVJXKUPUYDUYF
      UYEUUHRHZKIZUXSUYCUYETEZUUHTEZUYJUYFMZUYBUYCUWGUYGUYKQUYHFUUBUTVHUYBUWGUU
      GTEZUYLQUUGVEZFUUGUTVHUYKUYLUWOUYMSUYEUUHKXLXMWQUYDUYJUUBUUGRHZUXRIZUXSUY
      BUYNUYGUYQUYJMUYCUYOUYHUYNUYGNZUYQFUYPGHZKIZUYJUYGUYNUYQUYTMZUYGUYNNUYPTE
      ZVUAUUBUUGXNUWOVUBUWGFXOXPZNZVUASUWGVUCQXQXSUYQUXRUYPIUWOVUBVUDXRUYTUYPUX
      RVGKUYPFXTWIWBPYAUYRUYSUYIKUYGUYNUYSUYIIZUWGUYGUYNVUEQFUUBUUGYBYCYAWHWJYD
      UYCUYBUYQUXSYEUYCUYBNUYPDEUYQUXSUUBUUGYFUYPUXRDYGYHYAYJYJYIXFYKYLYMYNUUKU
      UEMUEUUKUUFMUUOUUPUUKUUEYOUUKUUEYPUUKUUFYQYRYSWJ $.
      $( [19-Apr-2014] $) $( [3-Apr-2014] $)
  $}

  ${
    $d A a b c $.  $d B a b c $.
    $( The sum of two odds is even.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    opoe $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ -. 2 || B ) ) ->
       2 || ( A + B ) ) $=
      ( va vb cz wcel c2 cdivides wbr wn caddc co wa cmul c1 wceq cc 2cn ax-1cn
      wrex cv odd2np1 bi2anan9 reeanv zaddcl peano2zdi dvdsmul1 sylancr axaddcl
      2z zcn adddi mp3an13 syl mp3an1 opreq1d eqtrd mulid1i df-2 opreq2i syl6eq
      eqtri mpan add4 mpanr12 syl2an breqtrd opreq12 breq2d syl5ibcom rexlimivv
      mulcl sylbir syl6bi imp an4s ) AEFZBEFZGAHIJZGBHIJZGABKLZHIZVQVRMZVSVTMZW
      BWCWDGCUAZNLZOKLZAPZCETZGDUAZNLZOKLZBPZDETZMZWBVQVSWIVRVTWNCAUBDBUBUCWOWH
      WMMZDETCETWBWHWMCDEEUDWPWBCDEEWEEFZWJEFZMZGWGWLKLZHIWPWBWSGGWEWJKLZOKLZNL
      ZWTHWSGEFXBEFGXCHIUJWSXAWEWJUEUFGXBUGUHWQWEQFZWJQFZXCWTPWRWEUKWJUKXDXEMZX
      CWFWKKLZOOKLZKLZWTXFXCXGGONLZKLZXIXFXCGXANLZXJKLZXKXFXAQFZXCXMPZWEWJUIGQF
      ZXNOQFZXORSGXAOULUMUNXFXLXGXJKXPXDXEXLXGPRGWEWJULUOUPUQXJXHXGKXJGXHGRURUS
      VBUTVAXDWFQFZWKQFZXIWTPZXEXPXDXRRGWEVLVCXPXEXSRGWJVLVCXRXSMXQXQXTSSWFWKOO
      VDVEVFUQVFVGWPWTWAGHWGAWLBKVHVIVJVKVMVNVOVP $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The difference of two odds is even.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    omoe $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ -. 2 || B ) ) ->
       2 || ( A - B ) ) $=
      ( va vb cz wcel c2 cdivides wbr wn cmin co wa cv cmul c1 wceq wrex cc 2cn
      caddc odd2np1 bi2anan9 reeanv 2z zsubcl dvdsmul1 sylancr zcn mulcl ax-1cn
      mpan pnpcan2 mp3an3 syl2an subdi mp3an1 eqtr4d breqtrrd opreq12 syl5ibcom
      breq2d rexlimivv sylbir syl6bi imp an4s ) AEFZBEFZGAHIJZGBHIJZGABKLZHIZVH
      VIMZVJVKMZVMVNVOGCNZOLZPUALZAQZCERZGDNZOLZPUALZBQZDERZMZVMVHVJVTVIVKWECAU
      BDBUBUCWFVSWDMZDERCERVMVSWDCDEEUDWGVMCDEEVPEFZWAEFZMZGVRWCKLZHIWGVMWJGGVP
      WAKLZOLZWKHWJGEFWLEFGWMHIUEVPWAUFGWLUGUHWHVPSFZWASFZWKWMQWIVPUIWAUIWNWOMW
      KVQWBKLZWMWNVQSFZWBSFZWKWPQZWOGSFZWNWQTGVPUJULWTWOWRTGWAUJULWQWRPSFWSUKVQ
      WBPUMUNUOWTWNWOWMWPQTGVPWAUPUQURUOUSWGWKVLGHVRAWCBKUTVBVAVCVDVEVFVG $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The sum of an odd and an even is odd.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    opeo $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ 2 || B ) ) ->
      -. 2 || ( A + B ) ) $=
      ( vc va vb cz wcel c2 cdivides wbr wa caddc co cmul c1 wceq wrex mpan 2cn
      cc wn cv odd2np1 wb divides bi2anan9 reeanv zaddcl zcn adddi mp3an1 mulcl
      2z opreq1d ax-1cn add32 mp3an3 syl2an mulcom adantl opreq2d 3eqtrd opreq2
      eqeq1d rcla4ev syl11anc opreq12 eqeq2d rexbidv syl5ibcom rexlimivv sylbir
      syl6bi imp an4s ad2ant2r syl mpbird ) AFGZHAIJUAZKBFGZHBIJZKKZHABLMZIJUAZ
      HCUBZNMZOLMZWDPZCFQZVSWAVTWBWJVSWAKZVTWBKZWJWKWLHDUBZNMZOLMZAPZDFQZEUBZHN
      MZBPZEFQZKZWJVSVTWQWAWBXADAUCHFGWAWBXAUDUMEHBUERUFXBWPWTKZEFQDFQWJWPWTDEF
      FUGXCWJDEFFWMFGZWRFGZKZWHWOWSLMZPZCFQZXCWJXFWMWRLMZFGHXJNMZOLMZXGPZXIWMWR
      UHXDWMTGZWRTGZXMXEWMUIWRUIXNXOKZXLWNHWRNMZLMZOLMZWOXQLMZXGXPXKXROLHTGZXNX
      OXKXRPSHWMWRUJUKUNXNWNTGZXQTGZXSXTPZXOYAXNYBSHWMULRYAXOYCSHWRULRYBYCOTGYD
      UOWNXQOUPUQURXPXQWSWOLXOXQWSPZXNYAXOYESHWRUSRUTVAVBURXHXMCXJFWFXJPZWHXLXG
      YFWGXKOLWFXJHNVCUNVDVEVFXCXHWICFXCXGWDWHWOAWSBLVGVHVIVJVKVLVMVNVOWCWDFGZW
      EWJUDVSWAYGVTWBABUHVPCWDUCVQVR $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)

    $( The difference of an odd and an even is odd.  (Contributed by Scott
       Fenton, 7-Apr-2014.) $)
    omeo $p |- ( ( ( A e. ZZ /\ -. 2 || A ) /\ ( B e. ZZ /\ 2 || B ) ) ->
      -. 2 || ( A - B ) ) $=
      ( vc va vb cz wcel c2 cdivides wa cmin co cmul c1 caddc wceq wrex mpan cc
      2cn wbr wn cv odd2np1 wb divides bi2anan9 reeanv zsubcl zcn subdi opreq1d
      2z mp3an1 mulcl ax-1cn addsub mp3an2 syl2an mulcom opreq2d adantl 3eqtr2d
      opreq2 eqeq1d rcla4ev syl11anc opreq12 eqeq2d rexbidv syl5ibcom rexlimivv
      sylbir syl6bi imp an4s ad2ant2r syl mpbird ) AFGZHAIUAUBZJBFGZHBIUAZJJZHA
      BKLZIUAUBZHCUCZMLZNOLZWEPZCFQZVTWBWAWCWKVTWBJZWAWCJZWKWLWMHDUCZMLZNOLZAPZ
      DFQZEUCZHMLZBPZEFQZJZWKVTWAWRWBWCXBDAUDHFGWBWCXBUEUMEHBUFRUGXCWQXAJZEFQDF
      QWKWQXADEFFUHXDWKDEFFWNFGZWSFGZJZWIWPWTKLZPZCFQZXDWKXGWNWSKLZFGHXKMLZNOLZ
      XHPZXJWNWSUIXEWNSGZWSSGZXNXFWNUJWSUJXOXPJZXMWOHWSMLZKLZNOLZWPXRKLZXHXQXLX
      SNOHSGZXOXPXLXSPTHWNWSUKUNULXOWOSGZXRSGZYAXTPZXPYBXOYCTHWNUORYBXPYDTHWSUO
      RYCNSGYDYEUPWONXRUQURUSXPYAXHPXOXPXRWTWPKYBXPXRWTPTHWSUTRVAVBVCUSXIXNCXKF
      WGXKPZWIXMXHYFWHXLNOWGXKHMVDULVEVFVGXDXIWJCFXDXHWEWIWPAWTBKVHVIVJVKVLVMVN
      VOVPWDWEFGZWFWKUEVTWBYGWAWCABUIVQCWEUDVRVS $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)
  $}

  $( Condition for a prime dividing a square.  (Contributed by Scott Fenton,
     8-Apr-2014.) $)
  pdivsq $p |- ( ( P e. Prime /\ M e. ZZ ) -> ( P || M <-> P || ( M ^ 2 ) ) )
      $=
    ( cprime wcel cz wa c2 cexp co cdivides wbr cmul wb cc zcn sqval syl breq2d
    wceq adantl wo euclemma 3anidm23 oridm syl6bb bitr2d ) ACDZBEDZFZABGHIZJKZA
    BBLIZJKZABJKZUHUKUMMUGUHUJULAJUHBNDUJULSBOBPQRTUIUMUNUNUAZUNUGUHUMUOMABBUBU
    CUNUDUEUF $.
    $( [19-Apr-2014] $) $( [8-Apr-2014] $)

  ${
    $d K m n $.  $d M m n $.  $d N m n $.
    $( Exponentiation law for divisibility.  (Contributed by Scott Fenton,
       7-Apr-2014.) $)
    dvdspw $p |- ( ( K e. ZZ /\ M e. ZZ /\ N e. NN ) ->
    ( K || M -> K || ( M ^ N ) ) ) $=
      ( vm cz wcel cn cdivides wbr cmul co wceq cexp wa adantr zexpcl syl111anc
      syl11anc cc zcn w3a cv wrex divides 3adant3 simpl1 simpl3 iddvdsexp simpr
      wb nnnn0 3ad2ant3 dvdsmul2 wi zmulcl dvdstr mp2and adantl 3ad2ant1 mulexp
      cn0 breqtrrd opreq1 breq2d syl5ibcom rexlimdva sylbid ) AEFZBEFZCGFZUAZAB
      HIZDUBZAJKZBLZDEUCZABCMKZHIZVHVIVLVPUJVJDABUDUEVKVOVRDEVKVMEFZNZAVNCMKZHI
      VOVRVTAVMCMKZACMKZJKZWAHVTAWCHIZWCWDHIZAWDHIZVTVHVJWEVHVIVJVSUFZVHVIVJVSU
      GACUHRVTWBEFZWCEFZWFVTVSCVAFZWIVKVSUIVKWKVSVJVHWKVICUKULOZVMCPRZVTVHWKWJW
      HWLACPRZWBWCUMRVTVHWJWDEFZWEWFNWGUNWHWNVTWIWJWOWMWNWBWCUORAWCWDUPQUQVTVMS
      FZASFZWKWAWDLVSWPVKVMTURVKWQVSVHVIWQVJATUSOWLVMACUTQVBVOWAVQAHVNBCMVCVDVE
      VFVG $.
      $( [19-Apr-2014] $) $( [7-Apr-2014] $)
  $}

  ${
    $d M m $.  $d P m $.
    $( If ` M ` divides a prime, then ` M ` is either the prime or one.
       (Contributed by Scott Fenton, 8-Apr-2014.) $)
    dvdsprime $p |- ( ( P e. Prime /\ M e. NN ) ->
       ( M || P <-> ( M = P \/ M = 1 ) ) ) $=
      ( vm cprime wcel cn wa cdivides wbr wceq c1 wo c2 cuz wi breq1 syl adantr
      eqeq1 syl5ibrcom cfv cv wral isprm2 orbi12d orcom syl6bb imbi12d rcla4cva
      adantll sylanb cz prmz iddvds 1dvds jaod impbid ) ADEZBFEZGZBAHIZBAJZBKJZ
      LZURAMNUAEZCUBZAHIZVFKJZVFAJZLZOZCFUCZGUSVAVDOZCAUDVLUSVMVEVKVMCBFVFBJZVG
      VAVJVDVFBAHPVNVJVCVBLVDVNVHVCVIVBVFBKSVFBASUEVCVBUFUGUHUIUJUKUTVBVAVCUTVA
      VBAAHIZURVOUSURAULEZVOAUMZAUNQRBAAHPTUTVAVCKAHIZURVRUSURVPVRVQAUOQRBKAHPT
      UPUQ $.
      $( [19-Apr-2014] $) $( [8-Apr-2014] $)
  $}

  $( Swap the second and third arguments of a gcd.  (Contributed by Scott
     Fenton, 8-Apr-2014.) $)
  gcd32 $p |- ( ( A e. ZZ /\ B e. ZZ /\ C e. ZZ ) ->
    ( ( A gcd B ) gcd C ) = ( ( A gcd C ) gcd B ) ) $=
    ( cz wcel w3a cgcd co wceq wa gcdcom opreq2d 3adant1 gcdass 3com23 3eqtr4d
    ) ADEZBDEZCDEZFABCGHZGHZACBGHZGHZABGHCGHACGHBGHZRSUAUCIQRSJTUBAGBCKLMCBANQS
    RUDUCIBCANOP $.
    $( [19-Apr-2014] $) $( [8-Apr-2014] $)

  $( Absorption law for gcd.  (Contributed by Scott Fenton, 8-Apr-2014.) $)
  gcdabsorb $p |- ( ( A e. ZZ /\ B e. ZZ ) -> ( ( A gcd B ) gcd B ) = ( A gcd B
      ) ) $=
    ( cz wcel wa cgcd co cabs wceq gcdass 3anidm23 gcdid opreq2d adantl gcdabs2
    cfv 3eqtrd ) ACDZBCDZEABFGZBFGZABBFGZFGZABHPZFGZTRSUAUCIBBAJKSUCUEIRSUBUDAF
    BLMNBAOQ $.
    $( [19-Apr-2014] $) $( [8-Apr-2014] $)

  ${
    $d M k n $.  $d N k n $.
    $( The GCD of a multiple of a number is the number itself.  (Contributed by
       Scott Fenton, 12-Apr-2014.) $)
    gcdmultiple $p |- ( ( M e. NN /\ N e. NN ) -> ( M gcd ( M x. N ) ) = M ) $=
      ( vk vn cn wcel cmul co cgcd wi c1 caddc opreq2 opreq2d eqeq1d imbi2d syl
      wceq cc cz cv weq cabs cfv nncn mulid1 nnz gcdid cr cc0 cle wbr cn0 nnnn0
      nnre nn0ge0 absid syl11anc 3eqtrd wa adantr zmulcl syl2an 1z mp3an1 ax1cn
      gcdaddm adddi mp3an3 ax-1cn axmulcom mpan2 eqtrd eqtr4d biimpd expcom a2d
      nnind impcom ) BEFAEFZAABGHZIHZARZVTAACUAZGHZIHZARZJVTAAKGHZIHZARZJVTAADU
      AZGHZIHZARZJVTAAWKKLHZGHZIHZARZJVTWCJCDBWDKRZWGWJVTWSWFWIAWSWEWHAIWDKAGMN
      OPCDUBZWGWNVTWTWFWMAWTWEWLAIWDWKAGMNOPWDWORZWGWRVTXAWFWQAXAWEWPAIWDWOAGMN
      OPWDBRZWGWCVTXBWFWBAXBWEWAAIWDBAGMNOPVTWIAAIHZAUCUDZAVTWHAAIVTASFZWHARAUE
      ZAUFQNVTATFZXCXDRAUGZAUHQVTAUIFUJAUKULZXDARAUOVTAUMFXIAUNAUPQAUQURUSWKEFZ
      VTWNWRVTXJWNWRJVTXJUTZWNWRXKWMWQAXKWMAWLKAGHZLHZIHZWQXKXGWLTFZWMXNRZVTXGX
      JXHVAVTXGWKTFXOXJXHWKUGAWKVBVCKTFXGXOXPVDKAWLVGVEURXKWPXMAIVTXEWKSFZWPXMR
      XJXFWKUEXEXQUTZWPWLWHLHZXMXEXQKSFZWPXSRVFAWKKVHVIXRWHXLWLLXEWHXLRZXQXEXTY
      AVJAKVKVLVANVMVCNVNOVOVPVQVRVS $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  ${
    $( If ` K ` and ` M ` are relatively prime, then the GCD of ` K ` and
       ` M x. N ` is the GCD of ` K ` and ` N ` .  (Contributed by Scott
       Fenton, 12-Apr-2014.) $)
    rprimemulgcd $p |- ( ( ( K e. NN /\ M e. NN /\ N e. NN ) /\
                  ( K gcd M ) = 1 ) -> ( K gcd ( M x. N ) ) = ( K gcd N ) ) $=
      ( cn wcel w3a cgcd co c1 wceq wa gcdmultiple 3adant2 cz nnz zmulcl syl2an
      cmul adantr eqtrd opreq1d 3ad2ant1 3adant1 gcdass syl111anc nnnn0 mulgcdr
      eqtr3d cn0 syl3an opreq1 sylan9eq cc nncn 3ad2ant3 mulid2 syl opreq2d ) A
      DEZBDEZCDEZFZABGHZIJZKZABCRHZGHZAACRHZVFGHZGHZACGHVBVGVJJVDVBAVHGHZVFGHZV
      GVJVBVKAVFGUSVAVKAJUTACLMUAVBANEZVHNEZVFNEZVLVJJUSUTVMVAAOZUBUSVAVNUTUSVM
      CNEZVNVAVPCOZACPQMUTVAVOUSUTBNEZVQVOVABOZVRBCPQUCVFVHAUDUEUHSVEVICAGVEVII
      CRHZCVBVDVIVCCRHZWAUSVMUTVSVACUIEVIWBJVPVTCUFABCUGUJVCICRUKULVECUMEZWACJV
      BWCVDVAUSWCUTCUNUOSCUPUQTURT $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  ${
    $d A n k $.  $d B n k $.  $d N n k $.
    $( If ` A ` and ` B ` are relatively prime, then so are ` A ^ N ` and
       ` B ` .  (Contributed by Scott Fenton, 12-Apr-2014.) $)
    rprimelpwr $p |- ( ( A e. NN /\ B e. NN /\ N e. NN ) ->
       ( ( A gcd B ) = 1 -> ( ( A ^ N ) gcd B ) = 1 ) ) $=
      ( cn wcel cgcd co c1 wceq cexp wi opreq2 opreq1d eqeq1d imbi2d syl adantr
      wa syl11anc cz vk vn cv caddc weq cc nncn exp1 biimpar df-3an cmul simpl1
      w3a cn0 simpl3 nnnn0 expp1 simp1 3ad2ant3 nnexpcl nnz mulcom eqtrd simpl2
      zcn opreq2d 3ad2ant1 3ad2ant2 gcdcom biimpa rprimemulgcd syl31anc 3eqtr4d
      peano2nn biimprd sylanbr an32s expcom a2d nnind exp3a com12 3impia ) ADEZ
      BDEZCDEZABFGZHIZACJGZBFGZHIZKZWFWDWERZWLWFWMWHWKWMWHRZAUAUCZJGZBFGZHIZKWN
      AHJGZBFGZHIZKWNAUBUCZJGZBFGZHIZKWNAXBHUDGZJGZBFGZHIZKWNWKKUAUBCWOHIZWRXAW
      NXJWQWTHXJWPWSBFWOHAJLMNOUAUBUEZWRXEWNXKWQXDHXKWPXCBFWOXBAJLMNOWOXFIZWRXI
      WNXLWQXHHXLWPXGBFWOXFAJLMNOWOCIZWRWKWNXMWQWJHXMWPWIBFWOCAJLMNOWMXAWHWMWTW
      GHWDWTWGIWEWDWSABFWDAUFEZWSAIAUGZAUHPMQNUIXBDEZWNXEXIWNXPXEXIKZWMXPWHXQWM
      XPRWDWEXPUMZWHXQWDWEXPUJXRWHRZXIXEXSXHXDHXSBXGFGZBXCFGZXHXDXSXTBAXCUKGZFG
      ZYAXSXGYBBFXSXGXCAUKGZYBXSXNXBUNEZXGYDIXSWDXNWDWEXPWHULZXOPZXSXPYEWDWEXPW
      HUOXBUPZPAXBUQSXSXCUFEZXNYDYBIXSXCTEZYIXRYJWHXRXCDEZYJXRWDYEYKWDWEXPURXPW
      DYEWEYHUSAXBUTSZXCVAPQZXCVEPYGXCAVBSVCVFXSWEWDYKBAFGZHIZYCYAIWDWEXPWHVDYF
      XRYKWHYLQXRWHYOXRWGYNHXRATEZBTEZWGYNIWDWEYPXPAVAVGWEWDYQXPBVAVHZABVISNVJB
      AXCVKVLVCXSXGTEZYQXHXTIXSXGDEZYSXSWDXFUNEZYTYFXSXFDEZUUAXRUUBWHXPWDUUBWEX
      BVNUSQXFUPPAXFUTSXGVAPXRYQWHYRQZXGBVISXSYJYQXDYAIYMUUCXCBVISVMNVOVPVQVRVS
      VTWAWBWC $.
      $( [19-Apr-2014] $) $( [12-Apr-2014] $)
  $}

  $( If ` A ` and ` B ` are relatively prime, then so are ` A ^ N ` and
     ` B ^ N ` .  (Contributed by Scott Fenton, 12-Apr-2014.) $)
  rprimepwr $p |- ( ( A e. NN /\ B e. NN /\ N e. NN ) ->
      ( ( A gcd B ) = 1 -> ( ( A ^ N ) gcd ( B ^ N ) ) = 1 ) ) $=
    ( cn wcel w3a cgcd co c1 wceq cexp wa syl syl11anc cz nnz gcdcom rprimelpwr
    3jca sylc simpl1 simpl2 simpl3 nnnn0 nnexpcl 3ad2ant1 3ad2ant2 eqeq1d eqtrd
    cn0 biimpa ex ) ADEZBDEZCDEZFZABGHZIJZACKHBCKHZGHIJZUPURLZUMUSDEZUOFAUSGHZI
    JUTVAUMVBUOUMUNUOURUAZVAUNCUJEZVBUMUNUOURUBZVAUOVEUMUNUOURUCZCUDMBCUENZVGSV
    AVCUSAGHZIVAAOEZUSOEZVCVIJVAUMVJVDAPZMVAVBVKVHUSPMAUSQNVAUNUMUOFBAGHZIJZVII
    JVAUNUMUOVFVDVGSUPURVNUPUQVMIUPVJBOEZUQVMJUMUNVJUOVLUFUNUMVOUOBPUGABQNUHUKB
    ACRTUIAUSCRTUL $.
    $( [19-Apr-2014] $) $( [12-Apr-2014] $)

  $( The GCD of one and an integer is one.  (Contributed by Scott Fenton,
     17-Apr-2014.) $)
  gcd12 $p |- ( M e. ZZ -> ( 1 gcd M ) = 1 ) $=
    ( cz wcel c1 cgcd co wceq 1z gcdcom mpan gcd1 eqtrd ) ABCZDAEFZADEFZDDBCMNO
    GHDAIJAKL $.
    $( [19-Apr-2014] $) $( [17-Apr-2014] $)

  ${
    $d A a b $.  $d B a b $.  $d C a b $.
    $( Division law for GCD. (Contributed by Scott Fenton, 18-Apr-2014.) $)
    gcddiv $p |- ( ( ( A e. ZZ /\ B e. ZZ /\ C e. NN )
       /\ ( C || A /\ C || B ) ) ->
       ( ( A gcd B ) / C ) = ( ( A / C ) gcd ( B / C ) ) ) $=
      ( va vb cz wcel w3a cdivides wa cgcd cdiv wceq cmul wrex 3ad2ant3 divcan4
      co cc syl111anc cn wbr cv nnz simp1 divides syl11anc simp2 anbi12d reeanv
      wb syl6bbr cc0 wne cn0 gcdcl nn0cn syl 3adant3 nncn nnne0 mulgcdr syl3an3
      wi nnnn0 opreq1d zcn 3ad2ant1 3ad2ant2 opreq12d 3eqtr4d opreq1 opreqan12d
      opreq12 eqeq12d syl5ibcom 3expa expcom rexlimdvv sylbid imp ) AFGZBFGZCUA
      GZHZCAIUBZCBIUBZJZABKRZCLRZACLRZBCLRZKRZMZWEWHDUCZCNRZAMZEUCZCNRZBMZJZEFO
      DFOZWNWEWHWQDFOZWTEFOZJXBWEWFXCWGXDWECFGZWBWFXCUKWDWBXEWCCUDPZWBWCWDUEDCA
      UFUGWEXEWCWGXDUKXFWBWCWDUHECBUFUGUIWQWTDEFFUJULWDWBXBWNVDWCWDXAWNDEFFWOFG
      ZWRFGZJZWDXAWNVDZXGXHWDXJXGXHWDHZWPWSKRZCLRZWPCLRZWSCLRZKRZMXAWNXKWOWRKRZ
      CNRZCLRZXQXMXPXKXQSGZCSGZCUMUNZXSXQMXGXHXTWDXIXQUOGXTWOWRUPXQUQURUSWDXGYA
      XHCUTPZWDXGYBXHCVAPZXQCQTXKXLXRCLWDXGXHCUOGXLXRMCVEWOWRCVBVCVFXKXNWOXOWRK
      XKWOSGZYAYBXNWOMXGXHYEWDWOVGVHYCYDWOCQTXKWRSGZYAYBXOWRMXHXGYFWDWRVGVIYCYD
      WRCQTVJVKXAXMWJXPWMXAXLWICLWPAWSBKVNVFWQWTXNWKXOWLKWPACLVLWSBCLVLVMVOVPVQ
      VRVSPVTWA $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $( Extend ~ gcdmultiple so ` N ` can be an integer.  (Contributed by Scott
       Fenton, 18-Apr-2014.) $)
    gcdmultiplez $p |- ( ( M e. NN /\ N e. ZZ ) ->
       ( M gcd ( M x. N ) ) = M ) $=
      ( cn wcel cz wa cmul co cgcd wceq cc0 opreq2 opreq2d cabs cfv cc syl11anc
      syl adantr eqtrd eqeq1d wne nncn zcn absmul syl2an cle wbr nnre cn0 nnnn0
      cr nn0ge0 absid opreq1d simpll nn0z 3syl nnz zmulcl sylan gcdabs2 nnabscl
      gcdmultiple sylan2 anassrs 3eqtr3d mul01 nn0gcdid0 pm2.61ne ) ACDZBEDZFZA
      ABGHZIHZAJAAKGHZIHZAJBKBKJZVOVQAVRVNVPAIBKAGLMUAVMBKUBZFZAVNNOZIHZAABNOZG
      HZIHZVOAVMWBWEJVSVMWAWDAIVMWAANOZWCGHZWDVKAPDZBPDWAWGJVLAUCZBUDABUEUFVKWG
      WDJVLVKWFAWCGVKAULDKAUGUHZWFAJAUIVKAUJDZWJAUKZAUMRAUNQUOSTMSVTAEDZVNEDZWB
      VOJVTVKWKWMVKVLVSUPWLAUQURVMWNVSVKWMVLWNAUSABUTVASVNAVBQVKVLVSWEAJZVLVSFV
      KWCCDWOBVCAWCVDVEVFVGVMVQAKIHZAVKVQWPJZVLVKWHWQWIWHVPKAIAVHMRSVKWPAJZVLVK
      WKWRWLAVIRSTVJ $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d M n $.  $d N n $.
    $( If ` M ` divides ` N ` , then ` M ` is their GCD. (Contributed by Scott
       Fenton, 18-Apr-2014.) $)
    dvdsgcdid $p |- ( ( M e. NN /\ N e. NN ) ->
      ( M || N -> ( M gcd N ) = M ) ) $=
      ( vn cn wcel wa cdivides wbr cv cmul co wceq cz wrex wb nnz syl2an eqeq1d
      cgcd cc divides wi mulcom gcdmultiplez opreq2 syl5ibcom sylbird rexlimdva
      nncn zcn adantr sylbid ) ADEZBDEZFABGHZCIZAJKZBLZCMNZABSKZALZUMAMEBMEUOUS
      OUNAPBPCABUAQUMUSVAUBUNUMURVACMUMUPMEZFZURAUPJKZBLZVAVCVDUQBUMATEUPTEVDUQ
      LVBAUIUPUJAUPUCQRVCAVDSKZALVEVAAUPUDVEVFUTAVDBASUERUFUGUHUKUL $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Unidirectional form of ~ dvdssq .  (Contributed by Scott Fenton,
       19-Apr-2014.) $)
    dvdssqim $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
       ( M || N -> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( vk cz wcel wa cdivides wbr cv cmul co wceq wrex c2 cexp divides syl2anc
      zsqcl cc zcn wi dvdsmul2 sqmul breqtrrd opreq1 breq2d syl5ibcom rexlimdva
      adantr sylbid ) ADEZBDEZFABGHCIZAJKZBLZCDMZANOKZBNOKZGHZCABPUKUPUSUAULUKU
      OUSCDUKUMDEZFZUQUNNOKZGHUOUSVAUQUMNOKZUQJKZVBGUTVCDEUQDEUQVDGHUKUMRARVCUQ
      UBQUTUMSEASEVBVDLUKUMTATUMAUCQUDUOVBURUQGUNBNOUEUFUGUHUIUJ $.
      $( [19-Apr-2014] $) $( [19-Apr-2014] $)
  $}

  ${
    $( Square distributes over GCD. (Contributed by Scott Fenton,
       18-Apr-2014.) $)
    sqgcd $p |- ( ( M e. NN /\ N e. NN ) ->
           ( ( M gcd N ) ^ 2 ) = ( ( M ^ 2 ) gcd ( N ^ 2 ) ) ) $=
      ( cn wcel wa cgcd co c2 cexp c1 wceq cc0 nnz adantr syl11anc syl cdiv wbr
      cc cz cmul wn anim12i wne nnne0 df-ne intnanrd gcdn0cl nnsqcl nncn mulid1
      sylib cdivides adantl gcddvds syl2an simpld wi dvdssqim mpd simprd gcddiv
      syl32anc sqdiv syl111anc opreq12d syl31anc divid eqtr3d wb divides2 mpbid
      clt cr nnre nngt0 divgt0 syl22anc elnnz sylanbrc rprimepwr mp3an3 3eqtr2d
      2nn ax-1cn divmul mp3an2 syl12anc ) ACDZBCDZEZABFGZHIGZJUAGZWMAHIGZBHIGZF
      GZWKWMSDZWNWMKWKWMCDZWRWKWLCDZWSWKATDZBTDZEALKZBLKZEUBZWTWIXAWJXBAMZBMZUC
      WIXEWJWIXCXDWIALUDXCUBAUEALUFULUGNABUHOZWLUIPZWMUJPZWMUKPWKWQWMQGZJKZWNWQ
      KZWKXKWOWMQGZWPWMQGZFGZAWLQGZHIGZBWLQGZHIGZFGZJWKWOTDZWPTDZWSWMWOUMRZWMWP
      UMRZXKXPKWIYBWJWIWOCDZYBAUIZWOMPZNWJYCWIWJWPCDYCBUIWPMPZUNXIWKWLAUMRZYDWK
      YJWLBUMRZWIXAXBYJYKEZWJXFXGABUOUPZUQZWKWLTDZXAYJYDURWKWTYOXHWLMPZWIXAWJXF
      NZWLAUSOUTWKYKYEWKYJYKYMVAZWKYOXBYKYEURYPWJXBWIXGUNZWLBUSOUTWOWPWMVBVCWKX
      RXNXTXOFWKASDZWLSDZWLLUDZXRXNKWIYTWJAUJNWKWTUUAXHWLUJPZWKWTUUBXHWLUEPZAWL
      VDVEWKBSDZUUAUUBXTXOKWJUUEWIBUJUNUUCUUDBWLVDVEVFWKXQXSFGZJKZYAJKZWKWLWLQG
      ZUUFJWKXAXBWTYLUUIUUFKYQYSXHYMABWLVBVGWKUUAUUBUUIJKUUCUUDWLVHOVIWKXQCDZXS
      CDZUUGUUHURZWKXQTDZLXQVMRZUUJWKYJUUMYNWKYOUUBXAYJUUMVJYPUUDYQWLAVKVEVLWKA
      VNDZLAVMRZWLVNDZLWLVMRZUUNWIUUOWJAVONWIUUPWJAVPNWKWTUUQXHWLVOPZWKWTUURXHW
      LVPPZAWLVQVRXQVSVTWKXSTDZLXSVMRZUUKWKYKUVAYRWKYOUUBXBYKUVAVJYPUUDYSWLBVKV
      EVLWKBVNDZLBVMRZUUQUURUVBWJUVCWIBVOUNWJUVDWIBVPUNUUSUUTBWLVQVRXSVSVTUUJUU
      KHCDUULWDXQXSHWAWBOUTWCWKWQSDZWRWMLUDZXLXMVJZWKWQCDZUVEWKYBYCEWOLKZWPLKZE
      UBZUVHWIYBWJYCYHYIUCWIUVKWJWIUVIUVJWIWOLUDZUVIUBWIYFUVLYGWOUEPWOLUFULUGNW
      OWPUHOWQUJPXJWKWSUVFXIWMUEPUVEJSDWRUVFEUVGWEWQJWMWFWGWHVLVI $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $d M k $.  $d N k $.
    $( Lemma for ~ dvdssq .  (Contributed by Scott Fenton, 18-Apr-2014.) $)
    dvdssqlem $p |- ( ( M e. NN /\ N e. NN ) ->
             ( M || N <-> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( cn wcel wa cdivides wbr c2 cexp co cz wi syl2an cgcd wceq adantr nnsqcl
      nnz cr syl dvdssqim sqgcd dvdsgcdid imp eqtrd wb cc0 cle cn0 gcdcl nn0ge0
      nn0re nnre nnnn0 sq11 syl22anc mpbid gcddvds simprd eqbrtrrd ex impbid )
      ACDZBCDZEZABFGZAHIJZBHIJZFGZVCAKDZBKDZVFVILVDARZBRZABUAMVEVIVFVEVIEZABNJZ
      ABFVNVOHIJZVGOZVOAOZVNVPVGVHNJZVGVEVPVSOVIABUBPVEVIVSVGOZVCVGCDVHCDVIVTLV
      DAQBQVGVHUCMUDUEVEVQVRUFZVIVEVOSDZUGVOUHGZASDZUGAUHGZWAVEVOUIDZWBVCVJVKWF
      VDVLVMABUJMZVOULTVEWFWCWGVOUKTVCWDVDAUMPVCWEVDVCAUIDWEAUNAUKTPVOAUOUPPUQV
      NVOAFGZVOBFGZVEWHWIEZVIVCVJVKWJVDVLVMABURMPUSUTVAVB $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  ${
    $( Two numbers are divisible iff their squares are.  (Contributed by Scott
       Fenton, 18-Apr-2014.) $)
    dvdssq $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
             ( M || N <-> ( M ^ 2 ) || ( N ^ 2 ) ) ) $=
      ( cz wcel wa cdivides wbr c2 cexp co wb cc0 wceq cabs cfv cn syl2an zsqcl
      syl adantr breq1 breq1d bibi12d wne nnabscl breq2 breq2d dvdssqlem sylan2
      sq0i nnz simpl dvdsabsb nnsqcl cc zcn abssq adantl bitr4d 3bitr4d anassrs
      dvds0 2thd pm2.61ne sylan absdvdsb adantlr eqcomd bitrd an32s sqeq0 0dvds
      3bitr4rd ) ACDZBCDZEABFGZAHIJZBHIJZFGZKZLBFGZLVRFGZKZALALMZVPWAVSWBALBFUA
      WDVQLVRFAUJUBUCVNALUDZVOVTVNWEEZVOEZANOZBFGZWHHIJZVRFGZVPVSWFWHPDZVOWIWKK
      ZAUEWLVOEWMWHLFGZWJLFGZKZBLBLMZWIWNWKWOBLWHFUFWQVRLWJFBUJUGUCWLVOBLUDZWMW
      LVOWREZEZWHBNOZFGZWJXAHIJZFGZWIWKWSWLXAPDXBXDKBUEWHXAUHUIWLWHCDZVOWIXBKWS
      WHUKZVOWRULWHBUMQWTWKWJVRNOZFGZXDWLWJCDZVRCDZWKXHKWSWLWJPDXIWHUNWJUKSVOXJ
      WRBRZTWJVRUMQWSXDXHKWLWSXCXGWJFWSBUODZXCXGMVOXLWRBUPZTBUQSUGURUSUTVAWLWPV
      OWLXEWPXFXEWNWOWHVBXEXIWOWHRWJVBSVCSTVDVEVNVOVPWIKWEABVFVGWGVSVQNOZVRFGZW
      KWFVQCDZXJVSXOKVOVNXPWEARTXKVQVRVFQWFXOWKKVOWFXNWJVRFVNXNWJMWEVNWJXNVNAUO
      DWJXNMAUPAUQSVHTUBTVIUTVJVOWCVNVOVRLMZWQWBWAVOXLXQWQKXMBVKSVOXJWBXQKXKVRV
      LSBVLVMURVD $.
      $( [19-Apr-2014] $) $( [18-Apr-2014] $)
  $}

  $( Either ` A / ( A gcd B ) ` is odd or ` B / ( A gcd B ) ` is odd.
     (Contributed by Scott Fenton, 19-Apr-2014.) $)
  divgcdodd $p |- ( ( A e. NN /\ B e. NN ) ->
    ( -. 2 || ( A / ( A gcd B ) ) \/ -. 2 || ( B / ( A gcd B ) ) ) ) $=
    ( cn wcel wa c2 cgcd co cdiv cdivides wbr wn c1 cz wb nnz syl wceq syl11anc
    cc0 wi cprime 2prm nprmdvds1 ax-mp gcddvds syl2an simpl anim12i nnne0 df-ne
    wo wne sylib intnanrd adantr divides2 syl111anc mpbid simpr adantl dvdsgcdb
    gcdn0cl 2z mp3an1 gcddiv syl31anc cc nncn divid eqtr3d breq2d biimpd sylbid
    exp3a imp mtoi ex imor ) ACDZBCDZEZFAABGHZIHZJKZFBWCIHZJKZLZUAWELWHULWBWEWH
    WBWEEWGFMJKZFUBDWILUCFUDUEWBWEWGWIUAWBWEWGWIWBWEWGEZFWDWFGHZJKZWIWBWDNDZWFN
    DZWJWLOZWBWCAJKZWMWBWPWCBJKZEZWPVTANDZBNDZWRWAAPZBPZABUFUGZWPWQUHQWBWCNDZWC
    TUMZWSWPWMOWBWCCDZXDWBWSWTEATRZBTRZELZXFVTWSWAWTXAXBUIVTXIWAVTXGXHVTATUMXGL
    AUJATUKUNUOUPABVCSZWCPQZWBXFXEXJWCUJQZVTWSWAXAUPZWCAUQURUSWBWQWNWBWRWQXCWPW
    QUTQWBXDXEWTWQWNOXKXLWAWTVTXBVAZWCBUQURUSFNDWMWNWOVDFWDWFVBVESWBWLWIWBWKMFJ
    WBWCWCIHZWKMWBWSWTXFWRXOWKRXMXNXJXCABWCVFVGWBWCVHDZXEXOMRWBXFXPXJWCVIQXLWCV
    JSVKVLVMVNVOVPVQVRWEWHVSUN $.
    $( [19-Apr-2014] $) $( [19-Apr-2014] $)

  $( The GCD of two numbers is the same as the GCD of the left and their sum.
     (Contributed by Scott Fenton, 20-Apr-2014.) $)
  gcdadd $p |- ( ( M e. ZZ /\ N e. ZZ ) ->
  ( M gcd N ) = ( M gcd ( N + M ) ) ) $=
    ( cz wcel wa cgcd co c1 cmul caddc wceq 1z gcdaddm mp3an1 cc mulid2 opreq2d
    zcn syl adantr eqtrd ) ACDZBCDZEABFGZABHAIGZJGZFGZABAJGZFGZHCDUBUCUDUGKLHAB
    MNUBUGUIKZUCUBAODZUJARUKUFUHAFUKUEABJAPQQSTUA $.
    $( [20-Apr-2014] $)

