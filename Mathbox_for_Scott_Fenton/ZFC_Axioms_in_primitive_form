$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              ZFC Axioms in primitive form
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( ~ ax-ext without distinct variable conditions or defined symbols. $)
  axextprim $p |- -. A. x -. ( ( x e. y -> x e. z ) ->
               ( ( x e. z -> x e. y ) -> y = z ) ) $=
    ( wel wb weq wi wex wn wal axextnd wa dfbi2 imbi1i impexp bitri exbii
    df-ex mpbi ) ABDZACDZEZBCFZGZAHZTUAGZUATGZUCGGZIAJIZABCKUEUHAHUIUDUHAUDUFUG
    LZUCGUHUBUJUCTUAMNUFUGUCOPQUHARPS $.
    $( [13-Oct-2010] $)

  $( ~ ax-rep without distinct variable conditions or defined symbols. $)
  axrepprim $p |- -. A. x -. ( -. A. y -. A. z ( ph -> z = y ) ->
               A. z -. ( ( A. y z e. x -> -. A. x ( A. z x e. y ->
               -. A. y ph ) ) -> -. ( -. A. x ( A. z x e. y -> -. A. y ph )
               -> A. y z e. x ) ) ) $=
    ( weq wi wal wex wel wa wb wn axrepnd df-ex df-an exbii exnal bitri bibi2i
    dfbi1 albii imbi12i mpbi ) ADCEFDGZCHZDBICGZBCIDGZACGZJZBHZKZDGZFZBHZUDLCGL
    ZUFUGUHLFZBGLZFUQUFFLFLZDGZFZLBGLZABCDMUNUTBHVAUMUTBUEUOULUSUDCNUKURDUKUFUQ
    KURUJUQUFUJUPLZBHUQUIVBBUGUHOPUPBQRSUFUQTRUAUBPUTBNRUC $.
    $( [13-Oct-2010] $)

  $( ~ ax-un without distinct variable conditions or defined symbols. $)
  axunprim $p |- -. A. x -. A. y ( -. A. x ( y e. x -> -. x e. z )
               -> y e. x ) $=
    ( wel wa wex wi wal wn axunnd df-an exbii exnal bitri imbi1i albii df-ex
    mpbi ) BADZACDZEZAFZSGZBHZAFZSTIGZAHIZSGZBHZIAHIZABCJUEUIAFUJUDUIAUCUHBUBUG
    SUBUFIZAFUGUAUKASTKLUFAMNOPLUIAQNR $.
    $( [13-Oct-2010] $)

  $( ~ ax-pow without distinct variable conditions or defined symbols. $)
  axpowprim $p |- ( A. x -. A. y ( A. x ( -. A. z -. x e. y -> A. y x e. z )
               -> y e. x ) -> x = y ) $=
    ( weq wel wn wal wi wex axpownd df-ex imbi1i albii exbii bitri sylib con4i
    ) ABDZABEZFCGFZACEBGZHZAGZBAEZHZBGZFAGZRFSCIZUAHZAGZUDHZBGZAIZUGFZABCJUMUFA
    IUNULUFAUKUEBUJUCUDUIUBAUHTUASCKLMLMNUFAKOPQ $.
    $( [13-Oct-2010] $)

  $( ~ ax-reg without distinct variable conditions or defined symbols. $)
  axregprim $p |- ( x e. y ->
               -. A. x ( x e. y -> -. A. z ( z e. x -> -. z e. y ) ) ) $=
    ( wel wn wi wal wa wex axregnd df-an exbii exnal bitri sylib ) ABDZPCADCBDE
    FCGZHZAIZPQEFZAGEZABCJSTEZAIUARUBAPQKLTAMNO $.
    $( [13-Oct-2010] $)

  $( ~ ax-inf without distinct variable conditions or defined symbols. $)
  axinfprim $p |- -. A. x -. ( y e. z -> -. ( y e. x ->
               -. A. y ( y e. x -> -. A. z ( y e. z -> -. z e. x ) ) ) ) $=
    ( wel wa wex wi wal wn axinfnd df-an exbii exnal bitri imbi2i albii anbi2i
    df-ex mpbi ) BCDZBADZUATCADZEZCFZGZBHZEZGZAFZTUAUATUBIGZCHIZGZBHZIGIZGZIAHI
    ZABCJUIUOAFUPUHUOAUGUNTUGUAUMEUNUFUMUAUEULBUDUKUAUDUJIZCFUKUCUQCTUBKLUJCMNO
    PQUAUMKNOLUOARNS $.
    $( [13-Oct-2010] $)

  $( ~ ax-ac without distinct variable conditions or defined symbols. $)
  axacprim $p |- -. A. x -. A. y A. z ( A. x -. ( y e. z -> -. z e. w ) ->
                  -. A. w -. A. y -. ( ( -. A. w ( y e. z -> ( z e. w ->
                  ( y e. w -> -. w e. x ) ) ) -> y = w ) -> -. ( y = w ->
                  -. A. w ( y e. z -> ( z e. w -> ( y e. w -> -. w e. x )
                  ) ) ) ) ) $=
    ( wel wa wal wex weq wb wi wn axacnd df-an albii anass annim pm4.63 anbi2i
    bitr3i 3bitr2i exbii exnal bitri bibi1i dfbi1 df-ex imbi12i 2albii mpbi )
    BCEZCDEZFZAGZUMBDEZDAEZFZFZDHZBDIZJZBGZDHZKZCGBGZAHZUKULLKLZAGZUKULUOUPLKZK
    ZKZDGLZUTKUTVLKLKLZBGZLDGLZKZCGBGZLAGLZABCDMVFVQAHVRVEVQAVDVPBCUNVHVCVOUMVG
    AUKULNOVCVNDHVOVBVNDVAVMBVAVLUTJVMUSVLUTUSVKLZDHVLURVSDURUKULUQFZFUKVJLZFVS
    UKULUQPWAVTUKWAULVILZFVTULVIQWBUQULUOUPRSTSUKVJQUAUBVKDUCUDUEVLUTUFUDOUBVND
    UGUDUHUIUBVQAUGUDUJ $.
    $( [26-Oct-2010] $)

