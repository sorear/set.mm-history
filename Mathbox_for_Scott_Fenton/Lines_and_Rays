$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Lines and Rays
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Line LinesEE Ray $.

  $( Declare the constant for the line function. $)
  cline2 $a class Line $.

  $( Declare the constant for the ray function. $)
  cray $a class Ray $.

  $( Declare the constant for the set of all lines. $)
  clines2 $a class LinesEE $.

  ${
    $d a b l n $.
    $( Define the ` Line ` function.  This function generates the line passing
       through the distinct points ` a ` and ` b ` .  Adapted from definition
       6.14 of [Schwabhauser] p. 45. $)
    df-line2 $a |- Line = { <. <. a , b >. , l >. |
       E. n e. NN ( ( a e. ( EE ` n ) /\ b e. ( EE ` n ) /\ a =/= b ) /\
         l = [ <. a , b >. ] `' Colinear ) } $.
  $}

  ${
    $d p a r n x $.
    $( Define the ` Ray ` function.  This function generates the set of all
       points that lie on the ray starting at ` p ` and passing through
       ` a ` .  Definition 6.8 of [Schwabhauser] p. 44. $)
    df-ray $a |- Ray = { <. <. p , a >. , r >. |
       E. n e. NN ( ( p e. ( EE ` n ) /\ a e. ( EE ` n ) /\ p =/= a ) /\
         r = { x e. ( EE ` n ) | p OutsideOf <. a , x >. } ) } $.
  $}

  $( Define the set of all lines.  Definition 6.14, part 2 of [Schwabhauser]
     p. 45.  See ~ ellines for membership. $)
  df-lines2 $a |- LinesEE = ran Line $.

  ${
    $d a m $.  $d a n $.  $d a p $.  $d a r $.  $d a s $.  $d a x $.  $d m n $.
    $d m p $.  $d m r $.  $d m s $.  $d m x $.  $d n p $.  $d n r $.  $d n s $.
    $d n x $.  $d p r $.  $d p s $.  $d p x $.  $d r s $.  $d r x $.  $d s x $.

    $( Show that the ` Ray ` relationship is a function.  (Contributed by Scott
       Fenton, 21-Oct-2013.) $)
    funray $p |- Fun Ray $=
      ( vp vn va vr vx vm vs cray wfun cv cee cfv wcel w3a crab wceq wa cn wrex
      wi wne cop coutsideof wbr copab2 wmo weq wal reeanv simp1 axdimuniq fveq2
      rabeq syl eqeq2d anbi1d eqtr3 syl6bi an4s ex com3l syl2an com12 rexlimivv
      imp sylbir gen2 anbi2d rexbidv eleq2d 3anbi12d anbi12d cbvrexv syl6bb mo4
      eqeq1 mpbir funoprab df-ray funeqi ) HIAJZBJZKLZMZCJZWCMZWAWEUAZNZDJZWAWE
      EJUBUCUDZEWCOZPZQZBRSZACDUEZIWNACDWNDUFWNWAFJZKLZMZWEWQMZWGNZGJZWJEWQOZPZ
      QZFRSZQZDGUGZTZGUHDUHXHDGXFWMXDQZFRSBRSXGWMXDBFRRUIXIXGBFRRXIWBRMZWPRMZQZ
      XGWHWTWLXCXLXGTZWHWTQWLXCQZXMWHWDWRXNXMTWTWDWFWGUJWRWSWGUJXLWDWRQZXNXGXLX
      OXNXGTZXJWDXKWRXPXJWDQXKWRQQBFUGZXPWAWPWBUKXQXNWIXBPZXCQXGXQWLXRXCXQWKXBW
      IXQWCWQPWKXBPWBWPKULZWJEWCWQUMUNZUOUPWIXAXBUQURUNUSUTVAVBVEUSVCVDVFVGWNXE
      DGXGWNWHXAWKPZQZBRSXEXGWMYBBRXGWLYAWHWIXAWKVPVHVIYBXDBFRXQWHWTYAXCXQWDWRW
      FWSWGXQWCWQWAXSVJXQWCWQWEXSVJVKXQWKXBXAXTUOVLVMVNVOVQVRHWOEBDACVSVTVQ $.
      $( [19-Apr-2014] $) $( [21-Oct-2013] $)
  $}

  ${
    $d A a $.  $d a n $.  $d A n $.  $d a p $.  $d A p $.  $d a r $.  $d A r $.
    $d a x $.  $d A x $.  $d N a $.  $d N n $.  $d n p $.  $d N p $.  $d n r $.
    $d N r $.  $d n x $.  $d N x $.  $d P a $.  $d P n $.  $d P p $.  $d p r $.
    $d P r $.  $d p x $.  $d P x $.  $d r x $.
    $( Calculate the value of the ` Ray ` function.  (Contributed by Scott
       Fenton, 21-Oct-2013.) $)
    fvray $p |- ( ( N e. NN /\
           ( P e. ( EE ` N ) /\ A e. ( EE ` N ) /\ P =/= A ) ) ->
           ( P Ray A ) =
             { x e. ( EE ` N ) | P OutsideOf <. A , x >. } ) $=
      ( vp vn va vr cn wcel cee w3a wa cray cop cv coutsideof wbr crab wceq cfv
      wne co df-opr wrex copab2 eqid fveq2 eleq2d 3anbi12d rabeq eqeq2d anbi12d
      syl rcla4ev mpanr2 wb simpr1 simpr2 fvex rabex eleq1 neeq1 3anbi13d breq1
      rabbidv rexbidv neeq2 3anbi23d opeq1 breq2d eqeq1 anbi2d eloprabg syl2anc
      cvv mp3an3 mpbird df-br df-ray eleq2i bitri wfun wi funray funbrfv sylbir
      ax-mp syl5eq ) DIJZCDKUAZJZBWKJZCBUBZLZMZCBNUCCBOZNUAZCBAPZOZQRZAWKSZCBNU
      DWPWQXBOZEPZFPZKUAZJZGPZXFJZXDXHUBZLZHPZXDXHWSOZQRZAXFSZTZMZFIUEZEGHUFZJZ
      WRXBTZWPXTCXFJZBXFJZWNLZXBXAAXFSZTZMZFIUEZWJWOXBXBTZYHXBUGYGWOYIMFDIXEDTZ
      YDWOYFYIYJYBWLYCWMWNYJXFWKCXEDKUHZUIYJXFWKBYKUIUJYJYEXBXBYJXFWKTYEXBTYKXA
      AXFWKUKUNULUMUOUPWPWLWMXTYHUQZWJWLWMWNURWJWLWMWNUSWLWMXBVPJYLXAAWKDKUTVAZ
      XRYBXICXHUBZLZXLCXMQRZAXFSZTZMZFIUEYDXLYETZMZFIUEYHEGHCBXBWKWKVPXDCTZXQYS
      FIUUBXKYOXPYRUUBXGYBXJYNXIXDCXFVBXDCXHVCVDUUBXOYQXLUUBXNYPAXFXDCXMQVEVFUL
      UMVGXHBTZYSUUAFIUUCYOYDYRYTUUCXIYCYNWNYBXHBXFVBXHBCVHVIUUCYQYEXLUUCYPXAAX
      FUUCXMWTCQXHBWSVJVKVFULUMVGXLXBTZUUAYGFIUUDYTYFYDXLXBYEVLVMVGVNVQVOVRXTWQ
      XBNRZYAUUEXCNJXTWQXBNVSNXSXCAFHEGVTWAWBNWCUUEYAWDWEWQXBNYMWFWHWGUNWI $.
      $( [19-Apr-2014] $) $( [21-Oct-2013] $)
  $}

  ${
    $d a b $.  $d a k $.  $d a l $.  $d a m $.  $d a n $.  $d b k $.  $d b l $.
    $d b m $.  $d b n $.  $d k l $.  $d k m $.  $d k n $.  $d l m $.  $d l n $.
    $d m n $.
    $( Show that the ` Line ` relationship is a function.  (Contributed by
       Scott Fenton, 25-Oct-2013.) $)
    funline $p |- Fun Line $=
      ( va vn vb vl vm vk cline2 wfun cv cee cfv wcel w3a wceq wa cn weq wi wal
      wrex wne cop ccolin cec copab2 wmo reeanv eqtr3 ad2ant2l rexlimivv sylbir
      ccnv a1i eqeq1 anbi2d rexbidv fveq2 eleq2d 3anbi12d anbi1d cbvrexv syl6bb
      gen2 mo4 mpbir funoprab df-line2 funeqi ) GHAIZBIZJKZLZCIZVKLZVIVMUAZMZDI
      ZVIVMUBUCULUDZNZOZBPTZACDUEZHWAACDWADUFWAVIEIZJKZLZVMWDLZVOMZFIZVRNZOZEPT
      ZOZDFQZRZFSDSWNDFWLVTWJOZEPTBPTWMVTWJBEPPUGWOWMBEPPWOWMRVJPLWCPLOVSWIWMVP
      WGVQWHVRUHUIUMUJUKVCWAWKDFWMWAVPWIOZBPTWKWMVTWPBPWMVSWIVPVQWHVRUNUOUPWPWJ
      BEPBEQZVPWGWIWQVLWEVNWFVOWQVKWDVIVJWCJUQZURWQVKWDVMWRURUSUTVAVBVDVEVFGWBB
      ACDVGVHVE $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)
  $}


  ${
    $d A l $.  $d A n $.  $d A x $.  $d A y $.  $d l n $.  $d l x $.  $d l y $.
    $d n x $.  $d n y $.  $d x y $.
    $( When ` Line ` is applied with the same argument, the result is the empty
       set.  (Contributed by Scott Fenton, 28-Oct-2013.) $)
    linedegen $p |- ( A Line A ) = (/) $=
      ( vx vn vy vl cline2 cop wcel wn wceq cv wne w3a cec wa wrex wex cvv syl
      cn co cfv c0 df-opr cdm cee ccolin ccnv copab eqid df-ne con2bii mpbi mto
      simp3 intnanr a1i nrex wb eleq1 neeq1 3anbi13d opeq1 eceq2 eqeq2d anbi12d
      nex rexbidv exbidv neeq2 3anbi23d opeq2 opelopabg anidms cxp wrel relopab
      mtbiri wss df-rel sseli opelxp1 con3i pm2.61i copab2 df-line2 dmeqi eqtri
      dmoprab eleq2i mtbir ndmfv ax-mp ) AAFUAAAGZFUBZUCAAFUDWNFUEZHZIWOUCJWQWN
      BKZCKZUFUBZHZDKZWTHZWRXBLZMZEKZWRXBGZUGUHZNZJZOZCTPZEQZBDUIZHZARHZXOIXPXO
      AWTHZXQAALZMZXFWNXHNZJZOZCTPZEQZYCEYBCTYBIWSTHXSYAXSXRAAJZXRIAUJXRYEAAUKU
      LUMXQXQXRUOUNUPUQURVGXPXOYDUSXMXQXCAXBLZMZXFAXBGZXHNZJZOZCTPZEQYDBDAARRWR
      AJZXLYLEYMXKYKCTYMXEYGXJYJYMXAXQXDYFXCWRAWTUTWRAXBVAVBYMXIYIXFYMXGYHJXIYI
      JWRAXBVCXGYHXHVDSVEVFVHVIXBAJZYLYCEYNYKYBCTYNYGXSYJYAYNXCXQYFXRXQXBAWTUTX
      BAAVJVKYNYIXTXFYNYHWNJYIXTJXBAAVLYHWNXHVDSVEVFVHVIVMVNVRXOXPXOWNRRVOZHXPX
      NYOWNXNVPXNYOVSXMBDVQXNVTUMWAAARRWBSWCWDWPXNWNWPXLBDEWEZUEXNFYPCBDEWFWGXL
      BDEWIWHWJWKWNFWLWMWH $.
      $( [19-Apr-2014] $) $( [29-Oct-2013] $)
  $}


  ${
    $d A a $.  $d a b $.  $d A b $.  $d a l $.  $d A l $.  $d a n $.  $d A n $.
    $d A x $.  $d B a $.  $d B b $.  $d b l $.  $d B l $.  $d b n $.  $d B n $.
    $d B x $.  $d l n $.  $d N n $.
    $( Calculate the value of the ` Line ` function.  (Contributed by Scott
       Fenton, 25-Oct-2013.) $)
    fvline $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ A =/= B ) ) ->
       ( A Line B ) = { x | x Colinear <. A , B >. } ) $=
      ( va vn vb vl cn wcel cee w3a wa cline2 cop ccolin cv wceq wrex cvv fveq2
      cfv wne co ccnv cec wbr copab2 eqid eleq2d 3anbi12d anbi1d rcla4ev mpanr2
      cab simpr1 simpr2 colinearex cnvex ecexg ax-mp eleq1 neeq1 3anbi13d opeq1
      eceq2 syl eqeq2d anbi12d rexbidv neeq2 3anbi23d opeq2 eqeq1 anbi2d mp3an3
      eloprabg syl2anc mpbird df-opr df-br df-line2 eleq2i wfun funline funbrfv
      wb bitri wi sylbir syl5eq opex dfec2 vex brcnv abbii eqtri syl6eq ) DIJZB
      DKUBZJZCWTJZBCUCZLZMZBCNUDZBCOZPUEZUFZAQZXGPUGZAUOZXEXGXIOZEQZFQZKUBZJZGQ
      ZXPJZXNXRUCZLZHQZXNXROZXHUFZRZMZFISZEGHUHZJZXFXIRXEYIBXPJZCXPJZXCLZXIXIRZ
      MZFISZWSXDYMYOXIUIYNXDYMMFDIXODRZYLXDYMYPYJXAYKXBXCYPXPWTBXODKUAZUJYPXPWT
      CYQUJUKULUMUNXEXAXBYIYOWGZWSXAXBXCUPWSXAXBXCUQXAXBXITJZYRXHTJYSPURUSXGTXH
      UTVAZYGYJXSBXRUCZLZYBBXROZXHUFZRZMZFISYLYBXIRZMZFISYOEGHBCXIWTWTTXNBRZYFU
      UFFIUUIYAUUBYEUUEUUIXQYJXTUUAXSXNBXPVBXNBXRVCVDUUIYDUUDYBUUIYCUUCRYDUUDRX
      NBXRVEYCUUCXHVFVGVHVIVJXRCRZUUFUUHFIUUJUUBYLUUEUUGUUJXSYKUUAXCYJXRCXPVBXR
      CBVKVLUUJUUDXIYBUUJUUCXGRUUDXIRXRCBVMUUCXGXHVFVGVHVIVJUUGUUHYNFIUUGUUGYMY
      LYBXIXIVNVOVJVQVPVRVSYIXFXGNUBZXIBCNVTYIXGXINUGZUUKXIRZUULXMNJYIXGXINWANY
      HXMFEGHWBWCWHNWDUULUUMWIWEXGXINYTWFVAWJWKVGXIXGXJXHUGZAUOXLAXGXHBCWLZWMUU
      NXKAXGXJPUUOAWNWOWPWQWR $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)
  $}

  ${
    $d N x $.  $d A x $.  $d B x $.
    $( A line is a subset of the space its two points lie in.  (Contributed by
       Scott Fenton, 25-Oct-2013.) $)
    liness $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ A =/= B ) ) ->
       ( A Line B ) C_ ( EE ` N ) ) $=
      ( vx cn wcel cee cfv wne w3a wa cline2 co cop ccolin wbr cab fvline cvv
      cv wi vex a1i simp1 simp2 3jca colineardim1 sylan2 abssdv eqsstrd ) CEFZA
      CGHZFZBULFZABIZJZKZABLMDTZABNOPZDQULDABCRUQUSDULUPUKURSFZUMUNJUSURULFUAUP
      UTUMUNUTUPDUBUCUMUNUOUDUMUNUOUEUFURABCSULUGUHUIUJ $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)

    $( Alternate definition of a line.  (Contributed by Scott Fenton,
       25-Oct-2013.) $)
    fvline2 $p |- ( ( N e. NN /\
       ( A e. ( EE ` N ) /\ B e. ( EE ` N ) /\ A =/= B ) ) ->
       ( A Line B ) = { x e. ( EE ` N ) | x Colinear <. A , B >. } ) $=
      ( cn wcel cee cfv wne w3a wa cline2 co cv cop ccolin wbr cab cin crab wss
      fvline wceq liness eqsstr3d df-ss sylib eqtr4d dfrab2 syl6eqr ) DEFBDGHZF
      CUKFBCIJKZBCLMZANBCOPQZARZUKSZUNAUKTULUMUOUPABCDUBZULUOUKUAUPUOUCULUOUMUK
      UQBCDUDUEUOUKUFUGUHUNAUKUIUJ $.
      $( [19-Apr-2014] $) $( [25-Oct-2013] $)
  $}

  ${
    $d N x $.  $d P x $.  $d Q x $.  $d R x $.
    $( A line is composed of a point and the two rays emerging from it.
       Theorem 6.15 of [Schwabhauser] p. 45.  (Contributed by Scott Fenton,
       26-Oct-2013.) $)
    lineunray $p |- ( ( N e. NN /\
       ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ R e. ( EE ` N ) ) /\
       ( P =/= Q /\ P =/= R ) ) ->
       ( P Btwn <. Q , R >. ->
       ( P Line Q ) = ( ( ( P Ray Q ) u. { P } ) u. ( P Ray R ) ) ) ) $=
      ( vx wcel w3a wne wa cop cbtwn wbr cun wceq crab wo wb syl13anc adantr wi
      cn cee cfv cline2 co cray csn cv ccolin coutsideof simpl1 simpl21 simpl22
      w3o simpr brcolinear olc orcd simpl3l necomd simprl simprr 3jca btwnconn2
      a1i simpl23 syl122anc olcd expr btwncom syl6bi 3jaod sylbid colineartriv1
      mpd orc syl6 syl3anc breq1 syl5ibrcom btwncolinear3 btwncolinear5 simpl3r
      jaod btwnouttr btwncolinear4 btwncomand btwnexch3and btwncolinear2 impbid
      pm5.63 df-ne anbi1i andi bitr3i orbi2i syl6bb broutsideof2 3simpc simprrl
      wn bitri simprrr impbid2 bitrd orbi12d orbi2d bitr4d orcom an32s rabbidva
      or32 simp1 simp21 simp22 simp3l fvline2 fvray rabsn eqcomd uneq12d simp23
      syl simp3r unrab uneq1i eqtri syl6eq 3eqtr4d ex ) DUAFZADUBUCZFZBYLFZCYLF
      ZGZABHZACHZIZGZABCJKLZABUDUEZABUFUEZAUGZMZACUFUEZMZNYTUUAIZEUHZABJZUILZEY
      LOZABUUIJZUJLZUUIANZPZACUUIJUJLZPZEYLOZUUBUUGUUHUUKUUREYLYTUUIYLFZUUAUUKU
      URQYTUUTIZUUAIZUUKUUOUUNUUQPZPZUURUVBUUKUUOUUIAHZBAUUIJZKLZUUIUUJKLZPZIZU
      VECUVFKLZUUIACJKLZPZIZPZPZUVDUVBUUKUUOUVIUVMPZPZUVPUVBUUKUVRUVBUUKUVQUVRU
      VBUUKUVHAUUMKLZBUUIAJKLZUNZUVQUVAUUKUWAQZUUAUVAYKUUTYMYNUWBYKYPYSUUTUKZYT
      UUTUOZYMYNYOYKYSUUTULZYMYNYOYKYSUUTUMZUUIABDUPRSUVBUVHUVQUVSUVTUVHUVQTUVB
      UVHUVIUVMUVHUVGUQURVEUVAUUAUVSUVQUVAUUAUVSIZIZUVMUVIUWHBAHZUUAUVSGZUVMUWH
      UWIUUAUVSUVAUWIUWGUVAABYQYRYKYPUUTUSUTSUVAUUAUVSVAUVAUUAUVSVBVCUVAUWJUVMT
      ZUWGUVAYKYNYMYOUUTUWKUWCUWFUWEYMYNYOYKYSUUTVFZUWDBACUUIDVDVGSVOVHVIUVAUVT
      UVQTUUAUVAUVTUVGUVQUVAYKYNUUTYMUVTUVGQUWCUWFUWDUWEBUUIADVJRUVGUVIUVMUVGUV
      HVPURVKSVLVMUVQUUOUQVQUVBUUOUUKUVQUVAUUOUUKTUUAUVAUUKUUOAUUJUILZUVAYKYMYN
      UWMUWCUWEUWFABDVNVRUUIAUUJUIVSVTSUVBUVIUUKUVMUVAUVIUUKTUUAUVAUVGUUKUVHUVA
      YKYMUUTYNUVGUUKTUWCUWEUWDUWFAUUIBDWARUVAYKYMYNUUTUVHUUKTUWCUWEUWFUWDABUUI
      DWBRWDSUVBUVKUUKUVLUVAUUAUVKUUKUVAUUAUVKIZIZUVSUUKUWOYRUUAUVKGZUVSUWOYRUU
      AUVKUVAYRUWNYQYRYKYPUUTWCSUVAUUAUVKVAUVAUUAUVKVBVCUVAUWPUVSTZUWNUVAYKYNYM
      YOUUTUWQUWCUWFUWEUWLUWDBACUUIDWEVGSVOUVAUVSUUKTZUWNUVAYKYNUUTYMUWRUWCUWFU
      WDUWEBUUIADWFRSVOVIUVAUUAUVLUUKUVAUUAUVLIZIAUUIBJKLZUUKUVAUWSCUUIABDUWCUW
      LUWDUWEUWFUVAUWSUUIACDUWCUWDUWEUWLUVAUUAUVLVBWGUVAUWSABCDUWCUWEUWFUWLUVAU
      UAUVLVAWGWHUVAUWTUUKTZUWSUVAYKUUTYNYMUXAUWCUWDUWFUWEUUIBADWIRSVOVIWDWDWDW
      JUVRUUOUUOXAZUVQIZPUVPUUOUVQWKUXCUVOUUOUXCUVEUVQIUVOUVEUXBUVQUUIAWLWMUVEU
      VIUVMWNWOWPXBWQUVBUVCUVOUUOUVAUVCUVOQUUAUVAUUNUVJUUQUVNUVAUUNUWIUVEUVIGZU
      VJUVAYKYMYNUUTUUNUXDQUWCUWEUWFUWDBUUIADWRRUVAUXDUVJUWIUVEUVIWSYTUUTUVJUXD
      YTUUTUVJIZIZUWIUVEUVIUXFABYQYRYKYPUXEUSUTYTUUTUVEUVIWTYTUUTUVEUVIXCVCVIXD
      XEUVAUUQCAHZUVEUVMGZUVNUVAYKYMYOUUTUUQUXHQUWCUWEUWLUWDCUUIADWRRUVAUXHUVNU
      XGUVEUVMWSYTUUTUVNUXHYTUUTUVNIZIZUXGUVEUVMUXJACYQYRYKYPUXIWCUTYTUUTUVEUVM
      WTYTUUTUVEUVMXCVCVIXDXEXFSXGXHUVDUVCUUOPUURUUOUVCXIUUNUUQUUOXLXBWQXJXKYTU
      UBUULNZUUAYTYKYMYNYQUXKYKYPYSXMZYKYMYNYOYSXNZYKYMYNYOYSXOZYKYPYQYRXPZEABD
      XQRSUUHUUGUUNEYLOZUUOEYLOZMZUUQEYLOZMZUUSYTUUGUXTNUUAYTUUEUXRUUFUXSYTUUCU
      XPUUDUXQYTYKYMYNYQUUCUXPNUXLUXMUXNUXOEBADXRRYTUXQUUDYTYMUXQUUDNUXMEYLAXSY
      CXTYAYTYKYMYOYRUUFUXSNUXLUXMYKYMYNYOYSYBYKYPYQYRYDECADXRRYASUXTUUPEYLOZUX
      SMUUSUXRUYAUXSUUNUUOEYLYEYFUUPUUQEYLYEYGYHYIYJ $.
      $( [19-Apr-2014] $) $( [26-Oct-2013] $)
  $}

  ${
    $d N x $.  $d P x $.  $d Q x $.  $d S x $.
    $( If ` S ` lies on ` P Q ` , then ` P Q = P S ` .  Theorem 6.16 of
       [Schwabhauser] p. 45.  (Contributed by Scott Fenton, 27-Oct-2013.) $)
    lineelsb2 $p |- ( ( N e. NN /\
      ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) /\
      ( S e. ( EE ` N ) /\ P =/= S ) ) ->
        ( S e. ( P Line Q ) -> ( P Line Q ) = ( P Line S ) ) ) $=
      ( vx w3a wa cop wbr cbtwn syl13anc adantr syl122anc mpd simprl btwncomand
      wi expr simprr 3jca cn wcel cee cfv wne ccolin cv crab wceq cline2 co w3o
      wb simpl1 simpl3l simpl21 brcolinear biimpa simpr btwnconn3 btwncolinear3
      simpl22 wo imp btwncolinear5 btwnexch3and btwncolinear4 btwnexchand 3jaod
      sylbid simpl3r necomd btwnouttr2 btwnconn1 impbid btwncolinear2 btwnconn2
      simpl23 btwnouttr mp2and 3jaodan syldan adantrl an32s rabbidva ex fvline2
      jaod 3adant3 eleq2d breq1 elrab syl6bb simp1 simp21 simp3l simp3r eqeq12d
      3imtr4d ) DUAUBZADUCUDZUBZBXAUBZABUEZFZCXAUBZACUEZGZFZXFCABHZUFIZGZEUGZXJ
      UFIZEXAUHZXMACHZUFIZEXAUHZUIZCABUJUKZUBZXTACUJUKZUIXIXLXSXIXLGXNXQEXAXIXM
      XAUBZXLXNXQUMZXIYCGZXKYDXFYEXKCXJJIZABCHJIZBCAHJIZULZYDYEXKYIYEWTXFXBXCXK
      YIUMWTXEXHYCUNZXFXGWTXEYCUOZXBXCXDWTXHYCUPZXBXCXDWTXHYCVBZCABDUQKURYEYFYD
      YGYHYEYFGZXNXQYNXNXMXJJIZABXMHJIZBXMAHZJIZULZXQYEXNYSUMZYFYEWTYCXBXCYTYJX
      IYCUSZYLYMXMABDUQKZLYNYOXQYPYRYEYFYOXQYEYFYOGZGCAXMHZJIZXMXPJIZVCZXQYEUUC
      UUGYEWTXBXFYCXCUUCUUGQYJYLYKUUAYMACXMBDUTMVDYEUUGXQQZUUCYEUUEXQUUFYEWTXBY
      CXFUUEXQQZYJYLUUAYKAXMCDVAKZYEWTXBXFYCUUFXQQZYJYLYKUUAACXMDVEKZWHZLNRYEYF
      YPXQYEYFYPGZGACXMHJIZXQYEUUNBCAXMDYJYMYKYLUUAYEUUNCABDYJYKYLYMYEYFYPOPYEY
      FYPSVFYEUUOXQQZUUNYEWTXFYCXBUUPYJYKUUAYLCXMADVGKZLNRYEYFYRXQYEYFYRGZGUUEX
      QYEUURACBXMDYJYLYKYMUUAYEYFYROYEUURBXMADYJYMUUAYLYEYFYRSPVHYEUUIUURUUJLNR
      VIVJYNXQUUFUUOCYQJIZULZXNYEXQUUTUMZYFYEWTYCXBXFUVAYJUUAYLYKXMACDUQKZLYNUU
      FXNUUOUUSYEYFUUFXNYEYFUUFGZGYOXNYEUVCAXMCBDYJYLUUAYKYMYEYFUUFSYEYFUUFOVHY
      EYOXNQZUVCYEWTXBXCYCUVDYJYLYMUUAABXMDVEKZLNRYEYFUUOXNYEYFUUOGZGZYPXNUVGCA
      UEZCBAHJIZUUOFZYPUVGUVHUVIUUOYEUVHUVFYEACXFXGWTXEYCVKZVLZLYEUVFCABDYJYKYL
      YMYEYFUUOOPYEYFUUOSTYEUVJYPQZUVFYEWTXCXFXBYCUVMYJYMYKYLUUABCAXMDVMMLNYEYP
      XNQZUVFYEWTXCYCXBUVNYJYMUUAYLBXMADVGKZLNRYEYFUUSXNYEYFUUSGZGZBUUDJIZYOVCZ
      XNUVQXGYFUUEFZUVSUVQXGYFUUEYEXGUVPUVKLYEYFUUSOYEUVPCXMADYJYKUUAYLYEYFUUSS
      PTYEUVTUVSQZUVPYEWTXBXFXCYCUWAYJYLYKYMUUAACBXMDVNMLNYEUVSXNQZUVPYEUVRXNYO
      YEWTXBYCXCUVRXNQZYJYLUUAYMAXMBDVAKZUVEWHZLNRVIVJVOYEYGGZXNXQUWFXNYSXQYEYT
      YGUUBLUWFYOXQYPYRYEYGYOXQYEYGYOGZGAXMCHJIZXQYEUWGBXMACDYJYMUUAYLYKYEUWGXM
      ABDYJUUAYLYMYEYGYOSPYEYGYOOVFYEUWHXQQZUWGYEWTYCXFXBUWIYJUUAYKYLXMCADVPKLN
      RYEYGYPXQYEYGYPGZGZUUGXQUWKBAUEZYGYPFZUUGUWKUWLYGYPYEUWLUWJYEABXBXCXDWTXH
      YCVRZVLZLYEYGYPOYEYGYPSTYEUWMUUGQZUWJYEWTXCXBXFYCUWPYJYMYLYKUUABACXMDVQML
      NYEUUHUWJUUMLNRYEYGYRXQYEYGYRGZGZUUOXQUWRXDACBHJIZUVRFZUUOUWRXDUWSUVRYEXD
      UWQUWNLYEUWQABCDYJYLYMYKYEYGYROPYEUWQBXMADYJYMUUAYLYEYGYRSPTYEUWTUUOQZUWQ
      YEWTXFXBXCYCUXAYJYKYLYMUUACABXMDVSMLNYEUUPUWQUUQLNRVIVJUWFXQUUTXNYEUVAYGU
      VBLUWFUUFXNUUOUUSYEYGUUFXNYEYGUUFGZGAXMBHJIZXNYEUXBCXMABDYJYKUUAYLYMYEUXB
      XMACDYJUUAYLYKYEYGUUFSPYEUXBABCDYJYLYMYKYEYGUUFOPVFYEUXCXNQZUXBYEWTYCXCXB
      UXDYJUUAYMYLXMBADVPKLNRYEYGUUOXNYEYGUUOGZGZUVSXNUXFUVHUWSUUOFZUVSUXFUVHUW
      SUUOYEUVHUXEUVLLYEUXEABCDYJYLYMYKYEYGUUOOPYEYGUUOSTYEUXGUVSQZUXEYEWTXFXBX
      CYCUXHYJYKYLYMUUACABXMDVQMLNYEUWBUXEUWELNRYEYGUUSXNYEYGUUSGZGZYPXNUXJXGYG
      UUEFZYPUXJXGYGUUEYEXGUXIUVKLYEYGUUSOYEUXICXMADYJYKUUAYLYEYGUUSSPTYEUXKYPQ
      ZUXIYEWTXCXBXFYCUXLYJYMYLYKUUABACXMDVSMLNYEUVNUXIUVOLNRVIVJVOYEYHGZXNXQUX
      MXNYSXQYEYTYHUUBLUXMYOXQYPYRYEYHYOXQYEYHYOGZGUUFXQYEUXNAXMBCDYJYLUUAYMYKY
      EYHYOSYEUXNBCADYJYMYKYLYEYHYOOPVHYEUUKUXNUULLNRYEYHYPXQYEYHYPGZGZUUOXQUXP
      UWLYHYPFZUUOUXPUWLYHYPYEUWLUXOUWOLYEYHYPOYEYHYPSTYEUXQUUOQZUXOYEWTXFXCXBY
      CUXRYJYKYMYLUUACBAXMDVMMLNYEUUPUXOUUQLNRYEYHYRXQYEYHYRGZGZUUGXQUXTXDBXPJI
      ZUVRFZUUGUXTXDUYAUVRYEXDUXSUWNLYEUXSBCADYJYMYKYLYEYHYROPYEUXSBXMADYJYMUUA
      YLYEYHYRSPTYEUYBUUGQZUXSYEWTXBXCXFYCUYCYJYLYMYKUUAABCXMDVNMLNYEUUHUXSUUML
      NRVIVJUXMXQUUTXNYEUVAYHUVBLUXMUUFXNUUOUUSYEYHUUFXNYEYHUUFGZGZUVSXNUYEUYAU
      UFUVSYEUYDBCADYJYMYKYLYEYHUUFOPYEYHUUFSYEUYAUUFGUVSQZUYDYEWTXBXCYCXFUYFYJ
      YLYMUUAYKABXMCDUTMLVTYEUWBUYDUWELNRYEYHUUOXNYEYHUUOGZGYPXNYEUYGCBAXMDYJYK
      YMYLUUAYEYHUUOOYEYHUUOSVFYEUVNUYGUVOLNRYEYHUUSXNYEYHUUSGZGUVRXNYEUYHABCXM
      DYJYLYMYKUUAYEUYHBCADYJYMYKYLYEYHUUSOPYEUYHCXMADYJYKUUAYLYEYHUUSSPVHYEUWC
      UYHUWDLNRVIVJVOWAWBWCWDWEWFXIYACXOUBXLXIXTXOCWTXEXTXOUIXHEABDWGWIZWJXNXKE
      CXAXMCXJUFWKWLWMXIXTXOYBXRUYIXIWTXBXFXGYBXRUIWTXEXHWNWTXBXCXDXHWOWTXEXFXG
      WPWTXEXFXGWQEACDWGKWRWS $.
      $( [19-Apr-2014] $) $( [27-Oct-2013] $)
  $}

  ${
    $d N x $.  $d P x $.  $d Q x $.
    $( Reflexivity law for line membership.  Part of theorem 6.17 of
       [Schwabhauser] p. 45.  (Contributed by Scott Fenton, 28-Oct-2013.) $)
    linerflx1 $p |- ( ( N e. NN /\
        ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) ) ->
        P e. ( P Line Q ) ) $=
      ( vx cn wcel cee cfv wne w3a wa cv cop ccolin wbr cline2 co colineartriv1
      crab simpr1 3adant3r3 breq1 elrab sylanbrc fvline2 eleqtrrd ) CEFZACGHZFZ
      BUHFZABIZJKZADLZABMZNOZDUHSZABPQULUIAUNNOZAUPFUGUIUJUKTUGUIUJUQUKABCRUAUO
      UQDAUHUMAUNNUBUCUDDABCUEUF $.
      $( [19-Apr-2014] $) $( [28-Oct-2013] $)

    $( Commutativity law for lines.  Part of theorem 6.17 of [Schwabhauser]
       p. 45.  (Contributed by Scott Fenton, 28-Oct-2013.) $)
    linecom $p |- ( ( N e. NN /\
        ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) ) ->
        ( P Line Q ) = ( Q Line P ) ) $=
      ( vx cn wcel cee cfv wne w3a wa cv cop ccolin wbr crab cline2 co fvline2
      wb simp1 simp3 simp21 simp22 colinearperm1 syl13anc rabbidva wceq 3anbi3i
      3expa necom 3ancoma bitri sylan2b 3eqtr4d ) CEFZACGHZFZBUQFZABIZJZKZDLZAB
      MNOZDUQPVCBAMNOZDUQPZABQRBAQRZVBVDVEDUQUPVAVCUQFZVDVETZUPVAVHJUPVHURUSVIU
      PVAVHUAUPVAVHUBUPURUSUTVHUCUPURUSUTVHUDVCABCUEUFUJUGDABCSVAUPUSURBAIZJZVG
      VFUHVAURUSVJJVKUTVJURUSABUKUIURUSVJULUMDBACSUNUO $.
      $( [19-Apr-2014] $) $( [28-Oct-2013] $)
  $}

  $( Reflexivity law for line membership.  Part of theorem 6.17 of
     [Schwabhauser] p. 45.  (Contributed by Scott Fenton, 28-Oct-2013.) $)
  linerflx2 $p |- ( ( N e. NN /\
        ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) ) ->
        Q e. ( P Line Q ) ) $=
    ( cn wcel cee cfv wne w3a wa cline2 necom 3anbi3i 3ancoma linerflx1 sylan2b
    co bitri linecom eleqtrrd ) CDEZACFGZEZBUBEZABHZIZJBBAKQZABKQUFUAUDUCBAHZIZ
    BUGEUFUCUDUHIUIUEUHUCUDABLMUCUDUHNRBACOPABCST $.
    $( [19-Apr-2014] $) $( [28-Oct-2013] $)

  ${
    $d A n $.  $d A p $.  $d A q $.  $d A x $.  $d n p $.  $d n q $.  $d n x $.
    $d p q $.  $d p x $.  $d q x $.
    $( Membership in the set of all lines.  (Contributed by Scott Fenton,
       28-Oct-2013.) $)
    ellines $p |- ( A e. LinesEE <->
             E. n e. NN E. p e. ( EE ` n ) E. q e. ( EE ` n )
                ( p =/= q /\ A = ( p Line q ) ) ) $=
      ( vx clines2 wcel cvv cv cline2 wceq wa cn eleq1 ccolin wex cab crn anass
      wrex wne co cee cfv elex wi oprex mpbiri adantl rexlimivw rexlimivv eqeq1
      a1i anbi2d rexbidv 2rexbidv w3a cop cec copab2 df-lines2 df-line2 rnoprab
      ccnv rneqi 3eqtri eleq2i abid simplrl simplrr simpll simpr 3jca jca simpl
      simpr2 simpr1 jca32 simpr3 impbii anbi1i 3bitr3ri fvline opex dfec2 brcnv
      wbr abbii eqtri syl6eqr eqeq2d pm5.32i 3exbii exrot3 bitr4i df-rex 2exbii
      vex 3bitrri r2ex r19.42v bitr3i bitri 3bitr4i 3bitri vtoclbg pm5.21nii )
      AFGZAHGZDIZCIZUAZAXJXKJUBZKZLZCBIZUCUDZTZDXQTBMTZAFUEXRXIBDMXQXRXIUFXPMGZ
      XJXQGZLZXOXICXQXNXIXLXNXIXMHGXJXKJUGAXMHNUHUIUJUMUKEIZFGZXLYCXMKZLZCXQTZD
      XQTBMTZXHXSEAHYCAFNYCAKZYGXRBDMXQYIYFXOCXQYIYEXNXLYCAXMULUNUOUPYDYCYAXKXQ
      GZXLUQZYCXJXKURZOVDZUSZKZLZBMTZCPDPZEQZGYRYHFYSYCFJRYQDCEUTZRYSVAJYTBDCEV
      BVEYQDCEVCVFVGYREVHXTYPLZBPZCPDPZYJYBYFLZLZCPZDPBPZYRYHUUCUUEBPCPDPUUGUUA
      UUEDCBUUEXTYKLZYELZUUHYOLUUAYJYBLZXLLZYELUUJYFLUUIUUEUUJXLYESUUKUUHYEUUKU
      UHUUKXTYKYJXTYAXLVIUUKYAYJXLYJXTYAXLVJYJYBXLVKUUJXLVLVMVNUUHUUJXLUUHYJXTY
      AXTYAYJXLVPXTYKVOXTYAYJXLVQVRXTYAYJXLVSVNVTWAYJYBYFSWBUUHYEYOUUHXMYNYCUUH
      XMYCYLOWGZEQZYNEXJXKXPWCYNYLYCYMWGZEQUUMEYLYMXJXKWDZWEUUNUULEYLYCOUUOEWRW
      FWHWIWJWKWLXTYKYOSWSWMUUEBDCWNWOYQUUBDCYPBMWPWQYHYBYGLZDPBPUUGYGBDMXQWTUU
      PUUFBDUUPUUDCXQTUUFYBYFCXQXAUUDCXQWPXBWQXCXDXEXFXG $.
      $( [19-Apr-2014] $) $( [28-Oct-2013] $)
  $}

  ${
    $d A a $.  $d a b $.  $d A b $.  $d a n $.  $d A n $.  $d b n $.  $d P a $.
    $d P b $.  $d P n $.  $d Q a $.  $d Q b $.  $d Q n $.
    $( If ` A ` is a line containing two distinct points ` P ` and ` Q ` , then
       ` A ` is the line through ` P ` and ` Q ` .  Theorem 6.18 of
       [Schwabhauser] p. 45.  (Contributed by Scott Fenton, 28-Oct-2013.) $)
    linethru $p |- ( ( A e. LinesEE /\ ( P e. A /\ Q e. A ) /\ P =/= Q ) ->
       A = ( P Line Q ) ) $=
      ( va vb vn wcel wa cline2 co wceq cv wrex cn wi syl13anc necomd lineelsb2
      wne syl132anc clines2 cee cfv ellines w3a simpll1 simpll2 simpll3 simprll
      wss simplr liness sseldd simprlr simplll adantl simprrl mpd linecom eqtrd
      anbi2d anbi1d opreq2 eqeq2d imbi12d mpbiri simp2l syl2anc simp1l1 simp1l2
      neeq2 simp1 simp1l3 simp1r simp2rr simp3 simpll syl eleqtrd simp2rl 3expa
      simp2lr 3eqtrd expcom pm2.61ine expr mp2and ex anbi12d syl5ibrcom expimpd
      eleq2 eqeq1 rexlimdva rexlimivv sylbi 3impib ) AUAGZBAGZCAGZHZBCSZABCIJZK
      ZWRDLZELZSZAXEXFIJZKZHZEFLZUBUCZMZDXLMFNMXAXBHZXDOZAFEDUDXMXOFDNXLXKNGZXE
      XLGZHXJXOEXLXPXQXFXLGZXJXOOXPXQXRUEZXGXIXOXSXGHZXOXIBXHGZCXHGZHZXBHZXHXCK
      ZOXTYDYEXTYDHZBXLGZCXLGZYEYFXHXLBYFXPXQXRXGXHXLUJXPXQXRXGYDUFXPXQXRXGYDUG
      XPXQXRXGYDUHXSXGYDUKXEXFXKULPZXTYAYBXBUIUMYFXHXLCYIXTYAYBXBUNZUMXTYDYGYHH
      ZYEXTYDYKHZHZYEOZCXECXEKZYNXTYCBXESZHZYKHZHZXHBXEIJZKZOYSXHXEBIJZYTYSYAXH
      UUBKZYRYAXTYAYBYPYKUOUPYSXPXQXRXGYGXEBSZYAUUCOXPXQXRXGYRUFZXPXQXRXGYRUGZX
      PXQXRXGYRUHXSXGYRUKXTYQYGYHUQZYSBXEXTYCYPYKUNQZXEXFBXKRTURYSXPXQYGUUDUUBY
      TKUUEUUFUUGUUHXEBXKUSPUTYOYMYSYEUUAYOYLYRXTYOYDYQYKYOXBYPYCCXEBVKVAVBVAYO
      XCYTXHCXEBIVCVDVEVFYMCXESZYEXTYLUUIYEXTYLUUIUEZXHCXEIJZCBIJZXCUUJXHXECIJZ
      UUKUUJYBXHUUMKZUUJXTYDYBXTYLUUIVLXTYDYKUUIVGZYJVHUUJXPXQXRXGYHXECSZYBUUNO
      XPXQXRXGYLUUIVIZXPXQXRXGYLUUIVJZXPXQXRXGYLUUIVMXSXGYLUUIVNYGYHYDXTUUIVOZU
      UJCXEXTYLUUIVPZQZXEXFCXKRTURUUJXPXQYHUUPUUMUUKKUUQUURUUSUVAXECXKUSPUTZUUJ
      BUUKGZUUKUULKZUUJBXHUUKUUJYDYAUUOYAYBXBVQVRUVBVSUUJXPYHXQUUIYGCBSZUVCUVDO
      UUQUUSUURUUTYGYHYDXTUUIVTZUUJBCYCXBYKXTUUIWBQZCXEBXKRTURUUJXPYHYGUVEUULXC
      KUUQUUSUVFUVGCBXKUSPWCWAWDWEWFWGWHXIXNYDXDYEXIXAYCXBXIWSYAWTYBAXHBWLAXHCW
      LWIVBAXHXCWMVEWJWKWAWNWOWPWQ $.
      $( [19-Apr-2014] $) $( [28-Oct-2013] $)
  $}

  ${
    $d N n $.  $d n p $.  $d N p $.  $d n q $.  $d N q $.  $d P n $.  $d P p $.
    $d p q $.  $d P q $.  $d P x $.  $d Q n $.  $d Q p $.  $d Q q $.  $d Q x $.
    $( There is a line through any two distinct points.  Hilbert's axiom I.1
       for geometry.  (Contributed by Scott Fenton, 29-Oct-2013.) $)
    hilbert1.1 $p |- ( ( N e. NN /\
       ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) ) ->
       E. x e. LinesEE ( P e. x /\ Q e. x ) ) $=
      ( vp vq vn cn wcel cee cfv wne wa cline2 co clines2 cv wrex wceq anbi12d
      w3a simp1 simp2 simp3 eqidd neeq1 opreq1 eqeq2d opreq2 rcla42ev syl112anc
      neeq2 rexeqdv rexeqbidv rcla4ev sylan2 ellines sylibr linerflx1 linerflx2
      fveq2 eleq2 syl12anc ) DHIZBDJKZIZCVEIZBCLZUAZMZBCNOZPIZBVKIZCVKIZBAQZIZC
      VOIZMZAPRVJEQZFQZLZVKVSVTNOZSZMZFGQZJKZRZEWFRZGHRZVLVIVDWDFVERZEVERZWIVIV
      FVGVHVKVKSZWKVFVGVHUBVFVGVHUCVFVGVHUDVIVKUEWDVHWLMBVTLZVKBVTNOZSZMEFBCVEV
      EVSBSZWAWMWCWOVSBVTUFWPWBWNVKVSBVTNUGUHTVTCSZWMVHWOWLVTCBULWQWNVKVKVTCBNU
      IUHTUJUKWHWKGDHWEDSZWGWJEWFVEWEDJVAZWRWDFWFVEWSUMUNUOUPVKGFEUQURBCDUSBCDU
      TVRVMVNMAVKPVOVKSVPVMVQVNVOVKBVBVOVKCVBTUOVC $.
      $( [19-Apr-2014] $) $( [29-Oct-2013] $)
  $}

  ${
    $d P x $.  $d P y $.  $d Q x $.  $d Q y $.  $d x y $.
    $( There is at most one line through any two distinct points.  Hilbert's
       axiom I.2 for geometry.  (Contributed by Scott Fenton, 29-Oct-2013.) $)
    hilbert1.2 $p |- ( P =/= Q ->
       E* x ( x e. LinesEE /\ ( P e. x /\ Q e. x ) ) ) $=
      ( vy wne cv wcel wa weq wi clines2 wral wceq simprl simprr simpl linethru
      syl3anc ex eleq2 wmo an4 cline2 anim12d eqtr3 syl6 syl5bi exp3a ralrimivv
      co anbi12d rmo4 sylibr ) BCEZBAFZGZCUOGZHZBDFZGZCUSGZHZHZADIZJZDKLAKLUOKG
      ZURHZAUAUNVEADKKUNVFUSKGZHZVCVDVIVCHVGVHVBHZHZUNVDVFVHURVBUBUNVKUOBCUCUJZ
      MZUSVLMZHVDUNVGVMVJVNUNVGVMUNVGHVFURUNVMUNVFURNUNVFUROUNVGPUOBCQRSUNVJVNU
      NVJHVHVBUNVNUNVHVBNUNVHVBOUNVJPUSBCQRSUDUOUSVLUEUFUGUHUIURVBADKVDUPUTUQVA
      UOUSBTUOUSCTUKULUM $.
      $( [19-Apr-2014] $) $( [29-Oct-2013] $)
  $}

  ${
    $d P x $.  $d Q x $.
    $( There is a unique line going through any two distinct points.  Theorem
       6.19 of [Schwabhauser] p. 46.  (Contributed by Scott Fenton,
       29-Oct-2013.) $)
    linethrueu $p |- ( ( N e. NN /\
       ( P e. ( EE ` N ) /\ Q e. ( EE ` N ) /\ P =/= Q ) ) ->
       E! x e. LinesEE ( P e. x /\ Q e. x ) ) $=
      ( cn wcel cee cfv wne w3a wa cv clines2 wrex hilbert1.1 simpr3 hilbert1.2
      wmo wreu syl reu5 sylanbrc ) DEFZBDGHZFZCUDFZBCIZJKZBALZFCUIFKZAMNUIMFUJK
      ARZUJAMSABCDOUHUGUKUCUEUFUGPABCQTUJAMUAUB $.
      $( [19-Apr-2014] $) $( [29-Oct-2013] $)
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Two distinct lines intersect in at most one point.  Theorem 6.21 of
       [Schwabhauser] p. 46.  (Contributed by Scott Fenton, 29-Oct-2013.) $)
    lineintmo $p |- ( ( A e. LinesEE /\ B e. LinesEE /\ A =/= B ) ->
       E* x ( x e. A /\ x e. B ) ) $=
      ( vy clines2 wcel wne w3a cv wa weq wi wal wmo an4 wceq linethru 3expa ex
      eleq1 cline2 co eqtr3 syl2an anandirs necon1d an4s sylan2b com23 alrimivv
      3impia anbi12d mo4 sylibr ) BEFZCEFZBCGZHZAIZBFZUSCFZJZDIZBFZVCCFZJZJZADK
      ZLZDMAMVBANURVIADUOUPUQVIUOUPJZVGUQVHVJVGUQVHLZVGVJUTVDJZVAVEJZJVKUTVAVDV
      EOUOVLUPVMVKUOVLJZUPVMJZJZUSVCBCVPUSVCGZBCPZVNVOVQVRVNVQJBUSVCUAUBZPZCVSP
      ZVRVOVQJUOVLVQVTBUSVCQRUPVMVQWACUSVCQRBCVSUCUDUESUFUGUHSUIUKUJVBVFADVHUTV
      DVAVEUSVCBTUSVCCTULUMUN $.
      $( [19-Apr-2014] $) $( [29-Oct-2013] $)
  $}

  $(
  @{
    @d a n @.  @d a p @.  @d a q @.  @d n p @.  @d n q @.  @d P a @.  @d P n @.
    @d P p @.  @d p q @.  @d P q @.  @d p x @.  @d P x @.  @d Q a @.  @d Q n @.
    @d Q p @.  @d Q q @.  @d q x @.  @d Q x @.  @d R a @.  @d R n @.  @d R p @.
    @d R q @.  @d R x @.
    @( Three points are colinear iff there is a line through all three of
       them.  Theorem 6.23 of [Schwabhauser] p. 46.  (Contributed by Scott
       Fenton, 7-Nov-2013). @)
    colinline @p |- ( ( Q e. V /\ R e. W ) -> ( P Colinear <. Q , R >. <->
          E. a e. LinesEE ( P e. a /\ Q e. a /\ R e. a ) ) ) @=
      ( vp vq vn vx wcel wa cop ccolin wbr cv w3a clines2 wrex wne cline2 co
      wceq cee cfv cn wi ellines df-3an cab fvline eqeq2d cvv eleq2 3anbi123d
      breq1 eqid elab4g 3anbi123i syl6bb pm5.32i 3an6 syl5bi adantld exp3a
      sylbid sylan2br expr imp3a anassrs rexlimdva rexlimivv sylbi rexlimiv
      impbid1 ) BDKCEKLABCMNOZAFPZKZBVQKZCVQKZQZFRS?WAVPFRVQRKGPZHPZTZVQWBWCUAU
      BZUCZLZHIPZUDUEZSZGWISIUFSWAVPUGZVQIHGUHWJWKIGUFWIWHUFKZWBWIKZLWGWKHWIWLW
      MWCWIKZWGWKUGWLWMWNLZLWDWFWKWLWOWDWFWKUGZWOWDLWLWMWNWDQZWPWMWNWDUIWLWQLZW
      FVQJPZWBWCMZNOZJUJZUCZWKWRWEXBVQJWBWCWHUKULWRXCWAVPXCWALXCAUMKZAWTNOZLZBU
      MKZBWTNOZLZCUMKZCWTNOZLZQZLWRVPXCWAXMXCWAAXBKZBXBKZCXBKZQXMXCVRXNVSXOVTXP
      VQXBAUNVQXBBUNVQXBCUNUOXNXFXOXIXPXLXAXEJAXBWSAWTNUPXBUQZURXAXHJBXBWSBWTNU
      PXQURXAXKJCXBWSCWTNUPXQURUSUTVAWRXMVPXCXMXDXGXJQXEXHXKQLWRVPXDXEXGXHXJXKV
      B?VCVDVCVEVFVGVHVIVJVKVLVMVNVO @.
      @( [19-Apr-2014] @) @( [7-Nov-2013] @)
  @}
  $)


