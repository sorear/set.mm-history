$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Hypothesis builders
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A more general form of ~ hbnt .  (Contributed by Scott Fenton,
     13-Dec-2010.) $)
  hbntg $p |- ( A. x ( ph -> A. x ps ) -> ( -. ps -> A. x -. ph ) ) $=
    ( wn wal wi a6o con1i con3 al2imi syl5 ) BDBCEZDZCEZALFZCEADZCENBBCGHOMPCAL
    IJK $.
    $( [13-Dec-2010] $)

  $( A more general and closed form of ~ hbim .  (Contributed by Scott Fenton,
     13-Dec-2010.) $)
  hbimtg $p |- ( ( A. x ( ph -> A. x ch ) /\ ( ps -> A. x th ) ) ->
                ( ( ch -> ps ) -> A. x ( ph -> th ) ) ) $=
    ( wal wi wa wn hbntg pm2.21 alimi syl6 adantr ax-1 imim2i adantl jad ) ACEF
    GEFZBDEFZGZHCBADGZEFZSCIZUCGUASUDAIZEFUCACEJUEUBEADKLMNUABUCGSTUCBDUBEDAOLP
    QR $.
    $( [13-Dec-2010] $)

  $( A more general and closed form of ~ hbal .  (Contributed by Scott Fenton,
     13-Dec-2010.) $)
  hbaltg $p |- ( A. x ( ph -> A. y ps ) -> ( A. x ph -> A. y A. x ps ) ) $=
    ( wal wi alim ax-7 imim2i ax-mp ) ABDEZFCEZACEZKCEZFZFLMBCEDEZFZFAKCGOQLNPM
    BCDHIIJ $.
    $( [13-Dec-2010] $)

  ${
    hbg.1 $e |- ( ph -> A. x ps ) $.
    $( A more general form of ~ hbn .  (Contributed by Scott Fenton,
       13-Dec-2010.) $)
    hbng $p |- ( -. ps -> A. x -. ph ) $=
      ( wal wi wn hbntg mpg ) ABCEFBGAGCEFCABCHDI $.
      $( [13-Dec-2010] $)

    ${
      hbg.2 $e |- ( ch -> A. x th ) $.
      $( A more general form of ~ hbim .  (Contributed by Scott Fenton,
         13-Dec-2010.) $)
      hbimg $p |- ( ( ps -> ch ) -> A. x ( ph -> th ) ) $=
        ( wal wi ax-gen hbimtg mp2an ) ABEHIZEHCDEHIBCIADIEHIMEFJGACBDEKL $.
        $( [13-Dec-2010] $)
    $}
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Membership in a range.  (Contributed by Scott Fenton, 2-Feb-2011.) $)
    elrn2g $p |- ( A e. V -> ( A e. ran B <-> E. x <. x , A >. e. B ) ) $=
      ( vy cv cop wcel wex crn wceq opeq2 eleq1d exbidv dfrn3 elab2g ) AFZEFZGZ
      CHZAIQBGZCHZAIEBCJDRBKZTUBAUCSUACRBQLMNAECOP $.
      $( [2-Feb-2011] $)

    $( Membership in a range.  (Contributed by Scott Fenton, 2-Feb-2011.) $)
    elrng $p |- ( A e. V -> ( A e. ran B <-> E. x x B A ) ) $=
      ( wcel crn cv cop wex wbr elrn2g wb df-br exbii a1i bitr4d ) BDEZBCFEAGZB
      HCEZAIZRBCJZAIZABCDKUBTLQUASARBCMNOP $.
      $( [2-Feb-2011] $)
  $}

