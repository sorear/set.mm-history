$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Hypothesis builders
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  $( A more general form of ~ hbnt . $)
  hbntg $p |- ( A. x ( ph -> A. x ps ) -> ( -. ps -> A. x -. ph ) ) $=
    ( wal wi wn con3 al2imi ax-6o con1i syl5 ) ABCDZEZCDLFZCDZAFZCDBFMNPCALGHOB
    BCIJK $.
    $( [15-Dec-2010] $) $( [13-Dec-2010] $)

  $( A more general and closed form of ~ hbim . $)
  hbimtg $p |- ( ( A. x ( ph -> A. x ch ) /\ ( ps -> A. x th ) ) ->
                ( ( ch -> ps ) -> A. x ( ph -> th ) ) ) $=
    ( wal wi wa wn hbntg pm2.21 alimi syl6 adantr ax-1 imim2i adantl jad ) ACEF
    GEFZBDEFZGZHCBADGZEFZSCIZUCGUASUDAIZEFUCACEJUEUBEADKLMNUABUCGSTUCBDUBEDAOLP
    QR $.
    $( [15-Dec-2010] $) $( [13-Dec-2010] $)

  $( A more general and closed form of ~ hbal . $)
  hbaltg $p |- ( A. x ( ph -> A. y ps ) -> ( A. x ph -> A. y A. x ps ) ) $=
    ( wal wi alim ax-7 imim2i ax-mp ) ABDEZFCEZACEZKCEZFZFLMBCEDEZFZFAKCGOQLNPM
    BCDHIIJ $.
    $( [15-Dec-2010] $) $( [13-Dec-2010] $)

  ${
    hbg.1 $e |- ( ph -> A. x ps ) $.
    $( A more general form of ~ hbn . $)
    hbng $p |- ( -. ps -> A. x -. ph ) $=
      ( wal wi wn hbntg mpg ) ABCEFBGAGCEFCABCHDI $.
      $( [15-Dec-2010] $) $( [13-Dec-2010] $)

    ${
      hbg.2 $e |- ( ch -> A. x th ) $.
      $( A more general form of ~ hbim . $)
      hbimg $p |- ( ( ps -> ch ) -> A. x ( ph -> th ) ) $=
        ( wal wi ax-gen hbimtg mp2an ) ABEHIZEHCDEHIBCIADIEHIMEFJGACBDEKL $.
        $( [15-Dec-2010] $) $( [13-Dec-2010] $)
    $}
  $}

  ${
    $d A x y $.  $d B x y $.
    $( Membership in a range. $)
    elrn2g $p |- ( A e. C -> ( A e. ran B <-> E. x <. x , A >. e. B ) ) $=
      ( vy cv cop wcel wex crn wceq opeq2 eleq1d exbidv dfrn3 elab2g ) AFZEFZGZ
      CHZAIQBGZCHZAIEBCJDRBKZTUBAUCSUACRBQLMNAECOP $.
      $( [2-Feb-2011] $)

    $( Membership in a range. $)
    elrng $p |- ( A e. C -> ( A e. ran B <-> E. x x B A ) ) $=
      ( wcel crn cv cop wex wbr elrn2g wb df-br exbii a1i bitr4d ) BDEZBCFEAGZB
      HCEZAIZRBCJZAIZABCDKUBTLQUASARBCMNOP $.
      $( [2-Feb-2011] $)
  $}

