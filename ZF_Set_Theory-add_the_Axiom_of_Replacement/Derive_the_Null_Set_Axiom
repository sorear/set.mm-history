$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Derive the Null Set Axiom
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( The Null Set Axiom of ZF set theory:  there exists a set with no
       elements.  Axiom of Empty Set of [Enderton] p. 18.  In some textbooks,
       this is presented as a separate axiom; here we show it can be derived
       from Separation ~ ax-sep .  It cannot be derived unless our predicate
       calculus asserts that at least one thing exists (see ~ a9e ).  This
       version of the Null Set Axiom tells us that at least one empty set
       exists, but does not tells us that it is unique - we need the Axiom
       of Extensionality to do that (see ~ zfnuleu ).

       This theorem should not be referenced by any proof.  Instead, use
       ~ ax-nul below so that the uses of the Null Set Axiom can be more easily
       identified. $)
    axnul $p |- E. x A. y -. y e. x $=
      ( vz cv wcel wn wa wb wal wex axsep2 pm3.24 id mtbiri 19.20i 19.22i
      ax-mp ) BDZADEZRCDEZTFZGZHZBIZAJSFZBIZAJUABACKUDUFAUCUEBUCSUBTLUCMNOPQ $.
      $( [7-Aug-2003] $)

    $( The Null Set Axiom of ZF set theory.  It was derived as ~ axnul above
       and is therefore redundant, but we state it as a separate axiom here so
       that its uses can be identified more easily. $)
    ax-nul $a |- E. x A. y -. y e. x $.

    $( Show the uniqueness of the empty set (using the Axiom of Extensionality
       via ~ bm1.1 to strengthen ~ ax-nul ). $)
    zfnuleu $p |- E! x A. y -. y e. x $=
      ( cv wcel wn wal weu wceq wb wex ax-nul equid nbn3 albii exbii mpbi
      ax-17 bm1.1 ax-mp eubii mpbir ) BCZACDZEZBFZAGUCUBUBHZEZIZBFZAGZUIAJZUJUE
      AJUKABKUEUIAUDUHBUFUCBLMNZOPUGABUGAQRSUEUIAULTUA $.
      $( [12-Sep-2006] $) $( [11-Sep-2006] $)

    $( The Null Set Axiom of ZF set theory:  the empty set exists.  Corollary
       5.16 of [TakeutiZaring] p. 20.  For the unabbreviated version, see
       ~ ax-nul .   $)
    0ex $p |- (/) e. V $=
      ( vx vy c0 cvv wcel cv wceq weu wn wal zfnuleu eq0 eubii mpbir eueq ) CDE
      AFZCGZAHZRBFPEIBJZAHABKQSABPLMNACON $.
      $( [5-Aug-1993] $)
  $}


