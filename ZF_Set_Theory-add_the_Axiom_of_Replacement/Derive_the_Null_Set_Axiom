$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Derive the Null Set Axiom
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    zfnuleu.1 $e |- E. x A. y -. y e. x $.
    $( Show the uniqueness of the empty set (using the Axiom of Extensionality
       via ~ bm1.1 to strengthen ~ axnul ). $)
    zfnuleu $p |- E! x A. y -. y e. x $=
      ( cv wcel wn wal weu wceq wb wex equid nbn3 albii exbii mpbi ax-17 bm1.1
      ax-mp eubii mpbir ) BDZADEZFZBGZAHUCUBUBIZFZJZBGZAHZUIAKZUJUEAKUKCUEUIAUD
      UHBUFUCBLMNZOPUGABUGAQRSUEUIAULTUA $.
      $( [22-Dec-2007] $) $( [22-Dec-2007] $)
  $}

  ${
    $d x y z w $.
    $( Prove ~ axnul directly from ~ ax-rep without using any equality axioms
       ( ~ ax-9 thru ~ ax-16 ).  The wff ` x = x ` substituted for ` ph ` in
       the ~ ax-rep instance is arbitrary.  Here, we don't need to know if
       ` x = x ` is true or false, only that it's not both.  (Contributed by
       Jeff Hoffman, 4-Feb-2008.) $)
    axnulALT $p |- E. x A. y -. y e. x $=
      ( vw vz cv wceq wn wb wal wi wex wcel wa ax-rep pm5.19 ax-4 mto intnan
      nex nbn albii exbii sylibr 19.8a pm2.21i mpg ) AEZUGFZUHGHZAIZBEZUGFZJZBI
      ZAKZUKUGLZGZBIZAKZCUOCIUPCEDELZUJMZCKZHZBIZAKUSUIDABCNURVDAUQVCBVBUPVACUJ
      UTUJUIUHOUIAPQZRSTUAUBUCUMUOBUNAUDUJULVEUEUFUF $.
      $( [5-Feb-2008] $) $( [4-Feb-2008] $)

    $( The Null Set Axiom of ZF set theory: there exists a set with no
       elements.  Axiom of Empty Set of [Enderton] p. 18.  In some textbooks,
       this is presented as a separate axiom; here we show it can be derived
       from Separation ~ ax-sep .  This version of the Null Set Axiom tells us
       that at least one empty set exists, but does not tells us that it is
       unique - we need the Axiom of Extensionality to do that (see
       ~ zfnuleu ).

       This proof, suggested by Jeff Hoffman (3-Feb-2008), does not require the
       set existence axiom ~ ax-9 , unlike some empty set existence proofs
       (such as the remark in [Kunen] p. 11).  However, it uses ~ ax-4 , which
       also presupposes the existence of at least one set, i.e it does not hold
       in a "free logic" valid in empty domains.  Theorem ~ ax4 , which shows
       the redundancy of ~ ax-4 , depends on ~ ax-9 for its proof.  See
       ~ axnulALT for a similar proof directly from ~ ax-rep .

       This theorem should not be referenced by any proof.  Instead, use
       ~ ax-nul below so that the uses of the Null Set Axiom can be more easily
       identified. $)
    axnul $p |- E. x A. y -. y e. x $=
      ( vz cv wcel wn wa wb wal wex ax-sep pm3.24 intnan id mtbiri 19.20i
      19.22i ax-mp ) BDZADEZSCDEZSSEZUBFGZGZHZBIZAJTFZBIZAJUCBACKUFUHAUEUGBUETU
      DUCUAUBLMUENOPQR $.
      $( [4-Feb-2008] $) $( [7-Aug-2003] $)

    $( The Null Set Axiom of ZF set theory.  It was derived as ~ axnul above
       and is therefore redundant, but we state it as a separate axiom here so
       that its uses can be identified more easily. $)
    ax-nul $a |- E. x A. y -. y e. x $.

    $( The Null Set Axiom of ZF set theory: the empty set exists.  Corollary
       5.16 of [TakeutiZaring] p. 20.  For the unabbreviated version, see
       ~ ax-nul . $)
    0ex $p |- (/) e. _V $=
      ( vx vy c0 cvv wcel cv wceq weu wn wal ax-nul zfnuleu eq0 eubii mpbir
      eueq ) CDEAFZCGZAHZSBFQEIBJZAHABABKLRTABQMNOACPO $.
      $( [5-Aug-1993] $)
  $}


