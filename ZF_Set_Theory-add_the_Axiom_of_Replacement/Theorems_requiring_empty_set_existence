$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Theorems requiring empty set existence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( Construct, from any class ` A ` , a set equal to it when the class
       exists and equal to the empty set when the class is proper.  This
       theorem shows that the constructed set always exists. $)
    class2set $p |- { x e. A | A e. _V } e. _V $=
      ( cvv wcel crab rabexg wn c0 wrex wceq cv pm3.26 nrexdv rabn0 necon1bbii
      sylib 0ex syl6eqel pm2.61i ) BCDZTABEZCDTABCFTGZUAHCUBTABIZGUAHJUBTABUBAK
      BDLMUCUAHTABNOPQRS $.
      $( [16-Oct-2003] $)

    $( Equality theorem based on ~ class2set .  (The proof was shortened by
       Raph Levien, 30-Jun-2006.) $)
    class2seteq $p |- ( A e. B -> { x e. A | A e. _V } = A ) $=
      ( wcel cvv crab wceq elisset wral cv ax-1 r19.21aiv rabid2 sylibr eqcomd
      syl ) BCDBEDZQABFZBGBCHQBRQQABIBRGQQABQAJBDKLQABMNOP $.
      $( [13-Dec-2005] $)
  $}

  $( Every power class contains the empty set. $)
  0elpw $p |- (/) e. ~P A $=
    ( c0 cpw wcel wss 0ss 0ex elpw mpbir ) BACDBAEAFBAGHI $.
    $( [25-Oct-2007] $)

  $( The empty set and its power set are not equal. $)
  0nep0 $p |- (/) =/= { (/) } $=
    ( c0 csn wne 0ex snnz necom mpbi ) ABZACAHCADEHAFG $.
    $( [13-Apr-2007] $) $( [23-Dec-1993] $)

  $( Something cannot be equal to both the null set and the power set of the
     null set. $)
  0inp0 $p |- ( A = (/) -> -. A = { (/) } ) $=
    ( c0 wceq csn wne wn 0nep0 neeq1 mpbiri df-ne sylib ) ABCZABDZEZAMCFLNBMEGA
    BMHIAMJK $.
    $( [30-Sep-2003] $)

  $( The removal of the empty set from a class does not affect its union. $)
  unidif0 $p |- U. ( A \ { (/) } ) = U. A $=
    ( c0 csn cdif cuni cun uniun undif1 uncom eqtr2i unieqi 0ex unisn uneq2i
    un0 3eqtr4ri uneq1i 3eqtri ) ABCZDZEZBAEZFZUBBFUBUASAFZEZSEZUBFUCTSFZEUAUFF
    ZUEUATSGUDUGUGASFUDASHASIJKUHUABFUAUFBUABLMZNUAOJPSAGUFBUBUIQRBUBIUBOR $.
    $( [22-Mar-2004] $)

  ${
    $d x y A $.  $d y B $.
    $( An indexed intersection of the empty set, with a non-empty index set, is
       empty. $)
    iin0 $p |- ( A =/= (/) <-> |^|_ x e. A (/) = (/) ) $=
      ( vy c0 wne ciin wceq cv wcel wral cab r19.3rzv abbi2dv df-iin syl6reqr
      cvv wn 0ex n0i ax-mp 0iin eqeq1i mtbir iineq1 eqeq1d mtbiri necon2ai
      impbii ) BDEZABDFZDGZUIDCHDIZABJZCKUJUIUMCDULABLMACBDNOUKBDBDGZUKADDFZDGZ
      UPPDGZDPIUQQRPDSTUOPDADUAUBUCUNUJUODABDDUDUEUFUGUH $.
      $( [20-Oct-2005] $)
  $}

  ${
    $d x A $.
    notzfaus.1 $e |- A = { (/) } $.
    notzfaus.2 $e |- ( ph <-> -. x e. y ) $.
    $( In the Separation Scheme ~ zfauscl , we require that ` y ` not occur in
       ` ph ` (which can be generalized to "not be free in").  Here we show
       that a contradiction can result if we omit this requirement. $)
    notzfaus $p |- -. E. y A. x ( x e. y <-> ( x e. A /\ ph ) ) $=
      ( cv wcel wa wb wal wn wex c0 wne csn 0ex snnz neeq1i mpbir n0 mpbi wi
      biimt iman anbi2i notbii bitr4i syl6bb xor3 sylibr eximi ax-mp exnal nex
      ) BGZCGHZUPDHZAIZJZBKZCUTLZBMZVALURBMZVCDNOZVDVENPZNONQRDVFNESTBDUAUBURVB
      BURUQUSLZJVBURUQURUQUCZVGURUQUDVHURUQLZIZLVGURUQUEUSVJAVIURFUFUGUHUIUQUSU
      JUKULUMUTBUNUBUO $.
      $( [8-Oct-2006] $) $( [8-Feb-2006] $)
  $}

  $( The intersection of the universal class is empty. $)
  intv $p |- |^| _V = (/) $=
    ( c0 cvv wcel cint wceq 0ex int0el ax-mp ) ABCBDAEFBGH $.
    $( [11-Sep-2008] $)

  ${
    $d x y z A $.
    axpweq.1 $e |- A e. _V $.
    $( Two equivalent ways to express the Power Set Axiom.  Note that ~ ax-pow
       is not used by the proof. $)
    axpweq $p |- ( ~P A e. _V
                <-> E. x A. y ( A. z ( z e. y -> z e. A ) -> y e. x ) ) $=
      ( cpw cvv wcel cv wex wi wal wss ssid elpwg mpbiri wceq pweq eleq2d
      cla4egv mpd elisset 19.23aiv impbii visset elpw2 pwss dfss2 imbi1i albii
      3bitri exbii bitri ) DFZGHZUNAIZFZHZAJZCIZBIZHUTDHKCLZVAUPHZKZBLZAJUOUSUO
      UNUNFZHZUSUOVGUNUNMUNNUNUNGOPURVGAUNGUPUNQUQVFUNUPUNRSTUAURUOAUNUQUBUCUDU
      RVEAURUNUPMVADMZVCKZBLVEUNUPAUEUFBDUPUGVIVDBVHVBVCCVADUHUIUJUKULUM $.
      $( [22-Jun-2009] $)
  $}

