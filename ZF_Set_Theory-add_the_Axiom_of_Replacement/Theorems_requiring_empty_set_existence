$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Theorems requiring empty set existence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x A $.
    $( Construct, from any class ` A ` , a set equal to it when the class
       exists and equal to the empty set when the class is proper.  This
       theorem shows that the constructed set always exists. $)
    class2set $p |- { x e. A | A e. _V } e. _V $=
      ( wcel crab rabexg wn c0 wrex wceq cv simpl nrexdv rabn0 necon1bbii sylib
      cvv 0ex syl6eqel pm2.61i ) BPCZTABDZPCTABPETFZUAGPUBTABHZFUAGIUBTABUBAJBC
      KLUCUAGTABMNOQRS $.
      $( [16-Oct-2003] $)

    $( Equality theorem based on ~ class2set .  (The proof was shortened by
       Raph Levien, 30-Jun-2006.) $)
    class2seteq $p |- ( A e. V -> { x e. A | A e. _V } = A ) $=
      ( wcel cvv crab wceq elisset wral ax-1 ralrimiv rabid2 sylibr eqcomd syl
      cv ) BCDBEDZQABFZBGBCHQBRQQABIBRGQQABQAPBDJKQABLMNO $.
      $( [13-Dec-2005] $)
  $}

  $( Every power class contains the empty set. $)
  0elpw $p |- (/) e. ~P A $=
    ( c0 cpw wcel wss 0ss 0ex elpw mpbir ) BACDBAEAFBAGHI $.
    $( [25-Oct-2007] $)

  $( The empty set and its power set are not equal. $)
  0nep0 $p |- (/) =/= { (/) } $=
    ( c0 csn 0ex snnz necomi ) ABAACDE $.
    $( [13-Apr-2007] $) $( [23-Dec-1993] $)

  $( Something cannot be equal to both the null set and the power set of the
     null set. $)
  0inp0 $p |- ( A = (/) -> -. A = { (/) } ) $=
    ( c0 wceq csn wne wn 0nep0 neeq1 mpbiri df-ne sylib ) ABCZABDZEZAMCFLNBMEGA
    BMHIAMJK $.
    $( [30-Sep-2003] $)

  $( The removal of the empty set from a class does not affect its union. $)
  unidif0 $p |- U. ( A \ { (/) } ) = U. A $=
    ( c0 csn cdif cuni cun uniun undif1 uncom eqtr2i unieqi 0ex uneq2i 3eqtr4ri
    unisn un0 uneq1i 3eqtri ) ABCZDZEZBAEZFZUBBFUBUASAFZEZSEZUBFUCTSFZEUAUFFZUE
    UATSGUDUGUGASFUDASHASIJKUHUABFUAUFBUABLOZMUAPJNSAGUFBUBUIQRBUBIUBPR $.
    $( [22-Mar-2004] $)

  ${
    $d x y A $.  $d y B $.
    $( An indexed intersection of the empty set, with a non-empty index set, is
       empty. $)
    iin0 $p |- ( A =/= (/) <-> |^|_ x e. A (/) = (/) ) $=
      ( vy c0 wne ciin wceq cv wcel cab r19.3rzv abbi2dv df-iin syl6reqr cvv wn
      wral 0ex n0i ax-mp 0iin eqeq1i mtbir iineq1 eqeq1d mtbiri necon2ai impbii
      ) BDEZABDFZDGZUIDCHDIZABQZCJUJUIUMCDULABKLACBDMNUKBDBDGZUKADDFZDGZUPODGZD
      OIUQPRODSTUOODADUAUBUCUNUJUODABDDUDUEUFUGUH $.
      $( [20-Oct-2005] $)
  $}

  ${
    $d x A $.
    notzfaus.1 $e |- A = { (/) } $.
    notzfaus.2 $e |- ( ph <-> -. x e. y ) $.
    $( In the Separation Scheme ~ zfauscl , we require that ` y ` not occur in
       ` ph ` (which can be generalized to "not be free in").  Here we show
       special cases of ` A ` and ` ph ` that result in a contradiction by
       violating this requirement. $)
    notzfaus $p |- -. E. y A. x ( x e. y <-> ( x e. A /\ ph ) ) $=
      ( cv wcel wa wb wal wn wex c0 wne csn 0ex snnz eqnetri mpbi n0 biimt iman
      wi anbi2i notbii bitr4i syl6bb xor3 sylibr eximi ax-mp exnal nex ) BGZCGH
      ZUODHZAIZJZBKZCUSLZBMZUTLUQBMZVBDNOVCDNPNENQRSBDUATUQVABUQUPURLZJVAUQUPUQ
      UPUDZVDUQUPUBVEUQUPLZIZLVDUQUPUCURVGAVFUQFUEUFUGUHUPURUIUJUKULUSBUMTUN $.
      $( [8-Oct-2006] $) $( [8-Feb-2006] $)
  $}

  $( The intersection of the universal class is empty. $)
  intv $p |- |^| _V = (/) $=
    ( c0 cvv wcel cint wceq 0ex int0el ax-mp ) ABCBDAEFBGH $.
    $( [11-Sep-2008] $)

  ${
    $d x y z A $.
    axpweq.1 $e |- A e. _V $.
    $( Two equivalent ways to express the Power Set Axiom.  Note that ~ ax-pow
       is not used by the proof. $)
    axpweq $p |- ( ~P A e. _V
                <-> E. x A. y ( A. z ( z e. y -> z e. A ) -> y e. x ) ) $=
      ( cpw cvv wcel cv wex wal wss ssid elpwg mpbiri wceq pweq eleq2d cla4egv
      wi mpd elisset exlimiv impbii visset elpw2 pwss dfss2 imbi1i albii 3bitri
      exbii bitri ) DFZGHZUNAIZFZHZAJZCIZBIZHUTDHTCKZVAUPHZTZBKZAJUOUSUOUNUNFZH
      ZUSUOVGUNUNLUNMUNUNGNOURVGAUNGUPUNPUQVFUNUPUNQRSUAURUOAUNUQUBUCUDURVEAURU
      NUPLVADLZVCTZBKVEUNUPAUEUFBDUPUGVIVDBVHVBVCCVADUHUIUJUKULUM $.
      $( [22-Jun-2009] $)
  $}

