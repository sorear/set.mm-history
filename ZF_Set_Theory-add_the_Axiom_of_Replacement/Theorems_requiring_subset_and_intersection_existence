$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Theorems requiring subset and intersection existence
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z $.
    $( No set contains all sets.  Theorem 41 of [Suppes] p. 30. $)
    nalset $p |- -. E. x A. y y e. x $=
      ( vz cv wcel wn wex wal alexn wa wb visset zfauscl wceq elequ1 elequ2
      bitrd notbid anbi12d bibi12d a4v pclem6 syl eximi ax-mp mpgbi ) BDZADZEZF
      ZBGZUIBHAGFAUIABICDZUGEZULUHEZULULEZFZJZKZCHZBGUKUPCBUHALMUSUJBUSUGUGEZUI
      UTFZJZKZUJURVCCBULUGNZUMUTUQVBCBBOVDUNUIUPVACBAOVDUOUTVDUOUGULEUTCBCOCBBP
      QRSTUAUTUIUBUCUDUEUF $.
      $( [23-Aug-1993] $)
  $}

  ${
    $d x y $.
    $( The universal class is not a member of itself (and thus is not a set).
       Proposition 5.21 of [TakeutiZaring] p. 21; our proof, however, does not
       depend on the Axiom of Regularity. $)
    vprc $p |- -. _V e. _V $=
      ( vx vy cvv wcel cv wceq wex wal nalset wb visset tbt albii dfcleq
      bitr4i exbii mtbi isset mtbir ) CCDAEZCFZAGZBEZTDZBHZAGUBABIUEUAAUEUDUCCD
      ZJZBHUAUDUGBUFUDBKLMBTCNOPQACRS $.
      $( [23-Aug-1993] $)
  $}

  $( The universal class doesn't belong to any class.  (Contributed by FL,
     31-Dec-2006.) $)
  nvel $p |- -. _V e. A $=
    ( cvv wcel vprc elisset mto ) BACBBCDBAEF $.
    $( [29-Dec-2006] $)

  $( The universal class does not exist. $)
  vnex $p |- -. E. x x = _V $=
    ( cvv wcel cv wceq wex vprc isset mtbi ) BBCADBEAFGABHI $.
    $( [4-Jul-2005] $)

  ${
    $d A x y $.  $d B x y $.
    inex1.1 $e |- A e. _V $.
    $( Separation Scheme (Aussonderung) using class notation.  Compare Exercise
       4 of [TakeutiZaring] p. 22. $)
    inex1 $p |- ( A i^i B ) e. _V $=
      ( vx vy cin cv wceq wex wcel wa wb wal zfauscl dfcleq elin bibi2i albii
      bitri exbii mpbir issetri ) DABFZDGZUCHZDIEGZUDJZUFAJUFBJZKZLZEMZDIUHEDAC
      NUEUKDUEUGUFUCJZLZEMUKEUDUCOUMUJEULUIUGUFABPQRSTUAUB $.
      $( [5-Aug-1993] $)
  $}

  ${
    inex2.1 $e |- A e. _V $.
    $( Separation Scheme (Aussonderung) using class notation. $)
    inex2 $p |- ( B i^i A ) e. _V $=
      ( cin cvv incom inex1 eqeltri ) BADABDEBAFABCGH $.
      $( [27-Apr-1994] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Closed-form, generalized Separation Scheme. $)
    inex1g $p |- ( A e. V -> ( A i^i B ) e. _V ) $=
      ( vx cv cin cvv wcel wceq ineq1 eleq1d visset inex1 vtoclg ) DEZBFZGHABFZ
      GHDACOAIPQGOABJKOBDLMN $.
      $( [7-Apr-1995] $)
  $}

  ${
    ssex.1 $e |- B e. _V $.
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring]
       p. 22.  This is one way to express the Axiom of Separation ~ ax-sep
       (a.k.a.  Subset Axiom). $)
    ssex $p |- ( A C_ B -> A e. _V ) $=
      ( wss cin wceq cvv wcel df-ss inex2 eleq1 mpbii sylbi ) ABDABEZAFZAGHZABI
      ONGHPBACJNAGKLM $.
      $( [27-Apr-1994] $)
  $}

  ${
    ssexi.1 $e |- B e. _V $.
    ssexi.2 $e |- A C_ B $.
    $( The subset of a set is also a set. $)
    ssexi $p |- A e. _V $=
      ( wss cvv wcel ssex ax-mp ) ABEAFGDABCHI $.
      $( [9-Sep-1993] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( The subset of a set is also a set.  Exercise 3 of [TakeutiZaring] p. 22
       (generalized). $)
    ssexg $p |- ( ( A C_ B /\ B e. C ) -> A e. _V ) $=
      ( vx wcel wss cvv cv wi wceq sseq2 imbi1d visset ssex vtoclg impcom ) BCE
      ABFZAGEZADHZFZRIQRIDBCSBJTQRSBAKLASDMNOP $.
      $( [11-Sep-2006] $) $( [14-Aug-1994] $)
  $}

  $( Existence of a difference. $)
  difexg $p |- ( A e. V -> ( A \ B ) e. _V ) $=
    ( cdif wss wcel cvv difss ssexg mpan ) ABDZAEACFKGFABHKACIJ $.
    $( [26-May-1998] $)

  ${
    $d x y A $.  $d y ph $.
    zfausab.1 $e |- A e. _V $.
    $( Separation Scheme (Aussonderung) in terms of a class abstraction. $)
    zfausab $p |- { x | ( x e. A /\ ph ) } e. _V $=
      ( cv wcel wa cab ssab2 ssexi ) BECFAGBHCDABCIJ $.
      $( [8-Jun-1994] $)
  $}

  ${
    $d x A $.
    $( Separation Scheme in terms of a restricted class abstraction. $)
    rabexg $p |- ( A e. V -> { x e. A | ph } e. _V ) $=
      ( crab wss wcel cvv ssrab2 ssexg mpan ) ABCEZCFCDGLHGABCILCDJK $.
      $( [23-Oct-1999] $)
  $}

  ${
    $d x A $.
    rabex.1 $e |- A e. _V $.
    $( Separation Scheme in terms of a restricted class abstraction. $)
    rabex $p |- { x e. A | ph } e. _V $=
      ( cvv wcel crab rabexg ax-mp ) CEFABCGEFDABCEHI $.
      $( [19-Jul-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    elssabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Membership in a class abstraction involving a subset.  Unlike ~ elabg ,
       ` A ` does not have to be a set. $)
    elssabg $p |- ( B e. V ->
                  ( A e. { x | ( x C_ B /\ ph ) } <-> ( A C_ B /\ ps ) ) ) $=
      ( wcel wss wa cvv wi cv cab wb ssexg expcom adantrd wceq sseq1 anbi12d
      elab3g syl ) EFHZDEIZBJZDKHZLDCMZEIZAJZCNHUFOUDUEUGBUEUDUGDEFPQRUJUFCDKUH
      DSUIUEABUHDETGUAUBUC $.
      $( [29-Aug-2007] $) $( [29-Aug-2006] $)
  $}

  $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
  elpw2g $p |- ( B e. V -> ( A e. ~P B <-> A C_ B ) ) $=
    ( wcel cpw wss elpwi cvv ssexg elpwg biimparc syldan expcom impbid2 ) BCDZA
    BEDZABFZABGQOPQOAHDZPABCIRPQABHJKLMN $.
    $( [7-Aug-2000] $)

  ${
    elpw2.1 $e |- B e. _V $.
    $( Membership in a power class.  Theorem 86 of [Suppes] p. 47. $)
    elpw2 $p |- ( A e. ~P B <-> A C_ B ) $=
      ( cvv wcel cpw wss wb elpw2g ax-mp ) BDEABFEABGHCABDIJ $.
      $( [11-Oct-2007] $)
  $}

  ${
    $d x A $.
    $( The intersection of a non-empty class exists.  Exercise 5 of
       [TakeutiZaring] p. 44 and its converse. $)
    intex $p |- ( A =/= (/) <-> |^| A e. _V ) $=
      ( vx c0 wne cint cvv wcel cv wex n0 wss intss1 visset ssex syl 19.23aiv
      sylbi wceq vprc inteq int0 syl6eq eleq1d mtbiri necon2ai impbii ) ACDZAEZ
      FGZUGBHZAGZBIUIBAJUKUIBUKUHUJKUIUJALUHUJBMNOPQUIACACRZUIFFGSULUHFFULUHCEF
      ACTUAUBUCUDUEUF $.
      $( [15-Mar-2007] $) $( [13-Aug-2002] $)
  $}

  $( If a class intersection is not a set, it must be the universe. $)
  intnex $p |- ( -. |^| A e. _V <-> |^| A = _V ) $=
    ( cint cvv wcel wn wceq c0 intex necon1bbii inteq int0 syl6eq sylbi vprc
    eleq1 mtbiri impbii ) ABZCDZEZRCFZTAGFZUASAGAHIUBRGBCAGJKLMUASCCDNRCCOPQ $.
    $( [3-Jul-2005] $)

  ${
    $d x y $.  $d ph y $.
    $( The intersection of a non-empty class abstraction exists. $)
    intexab $p |- ( E. x ph <-> |^| { x | ph } e. _V ) $=
      ( wex cab c0 wne cint cvv wcel abn0 intex bitr3i ) ABCABDZEFMGHIABJMKL $.
      $( [21-Oct-2003] $)
  $}

  $( The intersection of a non-empty restricted class abstraction exists. $)
  intexrab $p |- ( E. x e. A ph <-> |^| { x e. A | ph } e. _V ) $=
    ( cv wcel wa wex cab cint cvv wrex crab intexab df-rex df-rab inteqi
    eleq1i 3bitr4i ) BDCEAFZBGSBHZIZJEABCKABCLZIZJESBMABCNUCUAJUBTABCOPQR $.
    $( [21-Oct-2003] $)

  ${
    $d A x y $.  $d B y $.
    $( The existence of an indexed union. ` x ` is normally a free-variable
       parameter in ` B ` , which should be read ` B ( x ) ` .  (Contributed by
       FL, 19-Sep-2011.) $)
    iinexg $p |- ( ( A =/= (/) /\ A. x e. A B e. C )
    -> |^|_ x e. A B e. _V ) $=
      ( vy c0 wne wcel wral wa ciin cv wceq wrex cab cint cvv dfiin2g adantl
      wex wi elex a1i rgen r19.2z mpan2 r19.35 sylib imp rexcom4 abn0 sylibr
      intex eqeltrd ) BFGZCDHZABIZJZABCKZELCMZABNZEOZPZQUQUSVCMUOAEBCDRSURVBFGZ
      VCQHURVAETZVDURUTETZABNZVEUOUQVGUOUPVFUAZABNZUQVGUAUOVHABIVIVHABVHALBHECD
      UBUCUDVHABUEUFUPVFABUGUHUIUTAEBUJUHVAEUKULVBUMUHUN $.
      $( [19-Sep-2011] $)
  $}

  ${
    $d x y $.  $d x A $.  $d y ph $.  $d x ps $.  $d x ch $.
    intabs.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    intabs.2 $e |- ( x = |^| { y | ps } -> ( ph <-> ch ) ) $.
    intabs.3 $e |- ( |^| { y | ps } C_ A /\ ch ) $.
    $( Absorption of a redundant conjunct in the intersection of a class
       abstraction. $)
    intabs $p |- |^| { x | ( x C_ A /\ ph ) } = |^| { x | ph } $=
      ( cv wss wa cab cint cvv wcel wceq sseq1 anbi12d intmin3 wn intnex ssv
      sseq2 mpbiri sylbi pm2.61i cbvabv inteqi sseqtr4i simpr ss2abi intss
      ax-mp eqssi ) DJZFKZALZDMZNZADMZNZUTBEMZNZVBVDOPZUTVDKZURVDFKZCLDVDOUPVDQ
      UQVGACUPVDFRHSITVEUAVDOQZVFVCUBVHVFUTOKUTUCVDOUTUDUEUFUGVAVCABDEGUHUIUJUS
      VAKVBUTKURADUQAUKULUSVAUMUNUO $.
      $( [3-Jul-2005] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The intersection of a union ` U. A ` with a class ` B ` is equal to the
       union of the intersections of each element of ` A ` with ` B ` .
       (Contributed by FL, 24-Mar-2007.) $)
    inuni $p |- ( U. A i^i B ) = U. { x | E. y e. A x = ( y i^i B ) } $=
      ( vz cuni cin cv wceq wrex cab wcel wa wex eluni2 anbi1i elin ancom
      r19.41v bitr4i exbii rexcom4 visset inex1 eleq2 ceqsexv bitri rexbii
      3bitri 3bitr4i eluniab eqriv ) ECFZDGZAHZBHZDGZIZBCJZAKFZEHZUNLZVAUOLZUSM
      ZANZVAUTLVAUMLZVADLZMVAUPLZBCJZVGMZVBVEVFVIVGBVACOPVAUMDQVEURVCMZANZBCJZV
      HVGMZBCJVJVEVKBCJZANVMVDVOAVDUSVCMVOVCUSRURVCBCSTUAVKBACUBTVLVNBCVLVAUQLZ
      VNVCVPAUQUPDBUCUDUOUQVAUEUFVAUPDQUGUHVHVGBCSUIUJUSAVAUKTUL $.
      $( [25-Mar-2011] $) $( [24-Mar-2007] $)
  $}


