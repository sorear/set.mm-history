$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                   Ordered pair theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    opth1.1 $e |- A e. _V $.
    $( Equality of the first members of equal ordered pairs, which holds
       whether or not the second members are sets. $)
    opth1 $p |- ( <. A , B >. = <. C , D >. -> A = C ) $=
      ( cop wceq csn wcel cpr opi1 eleq2 mpbiri snex elop sylib snid prid1 jaoi
      wo syl elsnc ) ABFZCDFZGZACHZIZACGUEUFAHZGZUFABJZGZTZUGUEUFUCIZULUEUMUFUD
      ICDKUCUDUFLMUFABCNOPUIUGUKUIUGAUHIAEQUFUHALMUKUGAUJIABERUFUJALMSUAACEUBP
      $.
      $( [28-May-2008] $)
  $}

  ${
    opth.1 $e |- A e. _V $.
    opth.2 $e |- B e. _V $.
    opth.3 $e |- D e. _V $.
    $( The ordered pair theorem.  If two ordered pairs are equal, their first
       elements are equal and their second elements are equal.  Exercise 6 of
       [TakeutiZaring] p. 16.  Note that ` C ` is not required to be a set due
       to a peculiarity of our specific ordered pair definition. $)
    opth $p |- ( <. A , B >. = <. C , D >. <-> ( A = C /\ B = D ) ) $=
      ( cop wceq wa opth1 opeq1 eqeq1 syl5ib cpr csn df-op eqeq12i prex preqr2
      sylbi eqcomd syl syl6 jcai opeq12 impbii ) ABHZCDHZIZACIZBDIZJUJUKULABCDE
      KUJUKUICBHZIZULUKUHUMIUJUNACBLUHUIUMMNUNCDOZCBOZIZULUNCPZUOOZURUPOZIUQUIU
      SUMUTCDQCBQRUOUPURCDSCBSTUAUQDBDBCGFTUBUCUDUEABCDUFUG $.
      $( [28-May-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    opthg.1 $e |- A e. _V $.
    opthg.2 $e |- B e. _V $.
    $( Ordered pair theorem. $)
    opthg $p |- ( D e. V -> ( <. A , B >. = <. C , D >. <->
               ( A = C /\ B = D ) ) ) $=
      ( vx cop cv wceq wa opeq2 eqeq2d eqeq2 anbi2d vex opth vtoclbg ) ABIZCHJZ
      IZKACKZBUAKZLTCDIZKUCBDKZLHDEUADKZUBUETUADCMNUGUDUFUCUADBOPABCUAFGHQRS $.
      $( [30-Apr-1995] $)
  $}

  ${
    opthg2.1 $e |- C e. _V $.
    opthg2.2 $e |- D e. _V $.
    $( Ordered pair theorem. $)
    opthg2 $p |- ( B e. R -> ( <. A , B >. = <. C , D >. <->
               ( A = C /\ B = D ) ) ) $=
      ( wcel cop wceq wa opthg eqcom anbi12i 3bitr4g ) BEHCDIZABIZJCAJZDBJZKQPJ
      ACJZBDJZKCDABEFGLQPMTRUASACMBDMNO $.
      $( [21-Sep-2014] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x y X $.
    $( Ordered pair theorem. ` C ` is not required to be a set under our
       specific ordered pair definition. $)
    opthgg $p |- ( ( A e. V /\ B e. W /\ D e. X ) ->
                 ( <. A , B >. = <. C , D >. <-> ( A = C /\ B = D ) ) ) $=
      ( vx vy wcel cop wceq wa wb cv wi eqeq1d eqeq1 bibi12d imbi2d opeq1 opeq2
      anbi1d anbi2d vex opthg vtocl2g 3impia ) AEJBFJDGJZABKZCDKZLZACLZBDLZMZNZ
      UIHOZIOZKZUKLZUQCLZURDLZMZNZPUIAURKZUKLZUMVBMZNZPUIUPPHIABEFUQALZVDVHUIVI
      UTVFVCVGVIUSVEUKUQAURUAQVIVAUMVBUQACRUCSTURBLZVHUPUIVJVFULVGUOVJVEUJUKURB
      AUBQVJVBUNUMURBDRUDSTUQURCDGHUEIUEUFUGUH $.
      $( [14-Oct-2005] $)
  $}

  ${
    otthg.1 $e |- A e. _V $.
    otthg.2 $e |- B e. _V $.
    otthg.3 $e |- R e. _V $.
    $( Ordered triple theorem, with triple express with ordered pairs. $)
    otthg2 $p |- ( ( D e. V /\ S e. W ) ->
                ( <. <. A , B >. , R >. = <. <. C , D >. , S >. <->
                ( A = C /\ B = D /\ R = S ) ) ) $=
      ( wcel wa cop wceq w3a opex opthg anbi1d sylan9bbr df-3an syl6bbr ) DGLZF
      HLZMABNZENCDNZFNOZACOZBDOZMZEFOZMZUHUIUKPUDUGUEUFOZUKMUCULUEEUFFHABQKRUCU
      MUJUKABCDGIJRSTUHUIUKUAUB $.
      $( [1-May-1995] $)

    $( Ordered triple theorem. $)
    otthg $p |- ( ( D e. V /\ S e. W ) ->
     ( <. A , B , R >. = <. C , D , S >. <-> ( A = C /\ B = D /\ R = S ) ) ) $=
      ( cotp wceq cop wcel wa w3a df-ot eqeq12i otthg2 syl5bb ) ABELZCDFLZMABNE
      NZCDNFNZMDGOFHOPACMBDMEFMQUBUDUCUEABERCDFRSABCDEFGHIJKTUA $.
      $( [25-Sep-2014] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    eqvinop.1 $e |- B e. _V $.
    eqvinop.2 $e |- C e. _V $.
    $( A variable introduction law for ordered pairs.  Analog of Lemma 15 of
       [Monk2] p. 109. $)
    eqvinop $p |- ( A = <. B , C >. <-> E. x E. y ( A = <. x , y >. /\
                  <. x , y >. = <. B , C >. ) ) $=
      ( cv cop wceq wa wex vex opth anbi2i ancom 3bitri exbii eqeq2d ceqsexv
      anass 19.42v opeq2 opeq1 bitr2i ) CAHZBHZIZJZUHDEIZJZKZBLZALUFDJZCUFEIZJZ
      KZALCUJJZUMUQAUMUNUGEJZUIKZKZBLUNUTBLZKUQULVABULUIUNUSKZKVCUIKVAUKVCUIUFU
      GDEAMBMGNOUIVCPUNUSUIUAQRUNUTBUBVBUPUNUIUPBEGUSUHUOCUGEUFUCSTOQRUPURADFUN
      UOUJCUFDEUDSTUE $.
      $( [28-May-1995] $)
  $}

  ${
    $d x z w A $.  $d y z w A $.  $d z w ph $.
    $( Substitution of class ` A ` for ordered pair ` <. x , y >. ` .
       (Unnecessary distinct variable restrictions were removed by Andrew
       Salmon, 11-Jul-2011.) $)
    copsexg $p |- ( A = <. x , y >. ->
                  ( ph <-> E. x E. y ( A = <. x , y >. /\ ph ) ) ) $=
      ( vz vw cv cop wceq wa wex wb wi vex 19.8a syl5bi syl5 weu euequ1 eubii
      eqvinop 19.23bi ex opth anbi1i 2exbii hbe1 wal hbae anass a1i eximd biidd
      anim2d drex1 sylibd wn exbii 19.40 hbn1 dveeq2 anim1d syl6 pm2.61i exlimi
      19.9d equcom eupick com12 eqcom sylan9 sylbi impbid anbi1d 2exbidv bibi2d
      mpbi mpan eqeq1 imbi12d mpbiri adantr exlimivv pm2.43i ) DBGZCGZHZIZAWHAJ
      ZCKBKZLZWHDEGZFGZHZIZWNWGIZJZFKEKWHWKMZEFDWEWFBNCNZUAWQWREFWOWRWPWOWRWPAW
      PAJZCKZBKZLZMWPAXBWPAXBWTXBCXABOUBUCWPWLWEIZWMWFIZJZXBAMWLWMWEWFENFNWSUDZ
      XBXFAJZCKZBKZXFAWTXHBCWPXFAXGUEUFXJXDXEAJZCKZJZBKZXFAXIXNBXMBUGWFWEIZCUHZ
      XIXNMXPXIXMCKXNXPXHXMCCBCUIXHXDXKJZXPXMXDXEAUJZXPXKXLXDXKXLMXPXKCOUKUNPUL
      XMXMCBXPXMUMUOUPXPUQZXIXMXNXIXQCKZXSXMXHXQCXRURXTXDCKZXLJXSXMXDXKCUSXSYAX
      DXLXDXSCXOCUTCBEVAVFVBQPXMBOVCVDVEXDXNXLXEAXNXDXLXDBRZXNXDXLMWEWLIZBRYBBE
      SYCXDBBEVGTVQXDXLBVHVRVIXLXEAXECRZXLXEAMWFWMIZCRYDCFSYEXECWFWMVJTVQXEACVH
      VRVIVKQPVLVMWOWHWPWKXCDWNWGVSZWOWJXBAWOWIWTBCWOWHWPAYFVNVOVPVTWAWBWCVLWD
      $.
      $( [25-Jul-2011] $) $( [27-Dec-1996] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    $( Closed theorem form of ~ copsex2g . $)
    copsex2t $p |- ( ( A. x A. y ( ( x = A /\ y = B ) -> ( ph <-> ps ) )
      /\ ( A e. V /\ B e. W ) ) ->
                  ( E. x E. y ( <. A , B >. = <. x , y >. /\ ph ) <-> ps ) ) $=
      ( wcel wa cv wceq wb wal wex cop elisset hbe1 ax-17 hbbi wi anim12i eeanv
      sylibr hba1 hba2 hbex opeq12 copsexg eqcoms syl adantl 19.21bi imp bitr3d
      ax-4 ex exlimd sylan2 ) EGIZFHIZJZCKZELZDKZFLZJZABMZUAZDNZCNZVGDOZCOZEFPZ
      VCVEPZLAJZDOZCOZBMZVBVDCOZVFDOZJVMUTVTVAWACEGQDFHQUBVDVFCDUCUDVKVMVSVKVLV
      SCVJCUEVRBCVQCRBCSTVKVGVSDVIDCUFVRBDVQDCVPDRUGBDSTVKVGVSVKVGJAVRBVGAVRMZV
      KVGVOVNLWBVCVEEFUHWBVNVOACDVNUIUJUKULVKVGVHVKVIDVJCUPUMUNUOUQURURUNUS $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d x y ps $.  $d x y A $.  $d x y B $.
    copsex2g.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( Implicit substitution inference for ordered pairs. $)
    copsex2g $p |- ( ( A e. V /\ B e. W ) ->
                  ( E. x E. y ( <. A , B >. = <. x , y >. /\ ph ) <-> ps ) ) $=
      ( wcel cv wceq wex cop wa wb elisset hbe1 ax-17 hbbi eeanv opeq12 copsexg
      hbex eqcoms syl bitr3d exlimi sylbir syl2an ) EGJCKZELZCMZDKZFLZDMZEFNZUK
      UNNZLAOZDMZCMZBPZFHJCEGQDFHQUMUPOULUOOZDMZCMVBULUOCDUAVDVBCVABCUTCRBCSTVC
      VBDVABDUTDCUSDRUDBDSTVCAVABVCURUQLAVAPZUKUNEFUBVEUQURACDUQUCUEUFIUGUHUHUI
      UJ $.
      $( [28-May-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w D $.
    $d x y z w ps $.  $d x y z w R $.  $d x y z w S $.
    copsex4g.1 $e |- ( ( ( x = A /\ y = B ) /\ ( z = C /\ w = D ) ) ->
                     ( ph <-> ps ) ) $.
    $( An implicit substitution inference for 2 ordered pairs. $)
    copsex4g $p |- ( ( ( A e. R /\ B e. S ) /\ ( C e. R /\ D e. S ) ) ->
                      ( E. x E. y E. z E. w ( ( <. A , B >. = <. x , y >. /\
                      <. C , D >. = <. z , w >. ) /\ ph ) <-> ps ) ) $=
      ( wcel wa cop cv wceq wex vex wb eqcom opthg syl5bb adantl anbi1d 4exbidv
      bi2anan9 id cgsex4g bitrd ) GKNZHLNZOZIKNZJLNZOZOZGHPZCQZDQZPZRZIJPZEQZFQ
      ZPZRZOZAOZFSESDSCSUTGRVAHROZVEIRVFJROZOZAOZFSESDSCSBURVJVNCDEFURVIVMAUNVC
      VKUQVHVLUMVCVKUAULVCVBUSRUMVKUSVBUBUTVAGHLCTDTUCUDUEUPVHVLUAUOVHVGVDRUPVL
      VDVGUBVEVFIJLETFTUCUDUEUHUFUGABVMCDEFGHIJKLVMUIMUJUK $.
      $( [5-Aug-1995] $)
  $}

  $( An ordered pair is not empty. $)
  opnz $p |- -. <. A , B >. = (/) $=
    ( csn cop wcel c0 wceq wn opi1 n0i ax-mp ) ACZABDZEMFGHABIMLJK $.
    $( [24-Jan-2004] $)

  ${
    $d x A $.  $d x B $.
    $( A property of an ordered pair of proper classes (due to our particular
       definition of ordered pair). $)
    opprc1b $p |- ( -. A e. _V <-> (/) e. <. A , B >. ) $=
      ( vx cvv wcel wn c0 cop csn cpr 0ex prid1 opprc1 syl5eleqr wceq wne df-ne
      mpbi eqcom mtbi cv opeq1 eleq2d notbid vex snnz prnz pm3.2ni mtbir vtoclg
      wo elop con2i impbii ) ADEZFZGABHZEZUPGGBIZJUQGUSKLABMNUOURGCUAZBHZEZFURF
      CADUTAOZVBURVCVAUQGUTABUBUCUDVBGUTIZOZGUTBJZOZUKVEVGVDGOZVEVDGPVHFUTCUEZU
      FVDGQRVDGSTVFGOZVGVFGPVJFUTBVIUGVFGQRVFGSTUHGUTBKULUIUJUMUN $.
      $( [15-Nov-1994] $)
  $}

  $( A property of an ordered pair of proper classes (due to our particular
     definition of ordered pair). $)
  opprc3 $p |- ( ( -. A e. _V /\ -. B e. _V ) <-> <. A , B >. = { (/) } ) $=
    ( cvv wcel wn wa cop csn wceq cpr opprc2 opprc1 snprc preq2 sylbi sylan9eqr
    c0 eqtrd dfsn2 0ex syl6eqr eleq2 mpbiri opprc1b sylibr eqeq1d preqr2 eqeq2i
    snid snex 3imtr4i syl6bi anc2li mpcom impbii ) ACDEZBCDEZFZABGZQHZIZURUSQQJ
    ZUTUQUPUSAAGZVBABKUPVCQAHZJZVBAALUPVDQIVEVBIAMVDQQNORPQSZUAUPVAURVAQUSDZUPV
    AVGQUTDQTUIUSUTQUBUCABUDUEUPVAUQUPVAQBHZJZUTIZUQUPUSVIUTABLUFVIVBIVHQIVJUQV
    HQQBUJTUGUTVBVIVFUHBMUKULUMUNUO $.
    $( [17-May-1998] $)

  $( Equivalence of existence implied by equality of ordered pairs. $)
  opeqex $p |- ( <. A , B >. = <. C , D >. -> ( A e. _V <-> C e. _V ) ) $=
    ( cop wceq cvv wcel c0 wn eleq2 opprc1b 3bitr4g con4bid ) ABEZCDEZFZAGHZCGH
    ZQIOHIPHRJSJOPIKABLCDLMN $.
    $( [28-May-2008] $)

  $( Equivalence of existence implied by equality of ordered triples. $)
  oteqex $p |- ( <. <. A , B >. , C >. = <. <. R , S >. , T >. ->
           ( A e. _V <-> R e. _V ) ) $=
    ( cop wceq cvv wcel wb opex opth1 opeqex syl ) ABGZCGDEGZFGHPQHAIJDIJKPCQFA
    BLMABDENO $.
    $( [28-May-2008] $)

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    opth2.1 $e |- B e. _V $.
    opth2.2 $e |- D e. _V $.
    $( Equality of the second members of equal ordered pairs.  Because of our
       particular ordered pair definition, equality holds whether or not the
       first members are sets. $)
    opth2 $p |- ( <. A , B >. = <. C , D >. -> B = D ) $=
      ( vx cvv wcel cop wceq wi wn wa c0 opprc1b csn cpr opprc1 snex eqeq1d vex
      opeq1 imbi1d opth simprbi vtoclg con1bii bicomi nelneq2 syl2anb eqeqan12d
      cv pm2.21d preqr2 sneqr syl syl6bi pm2.61dan pm2.61i ) AHIZABJZCDJZKZBDKZ
      LZGUMZBJZVCKZVELVFGAHVGAKZVIVDVEVJVHVBVCVGABUCUAUDVIVGCKVEVGBCDGUBEFUEUFU
      GVAMZCHIZVFVKVLNVDVEVKOVBIOVCIZMZVDMVLABPVNVLVLVMCDPUHUIOVBVCUJUKUNVKVLMZ
      NVDOBQZRZODQZRZKZVEVKVOVBVQVCVSABSCDSULVTVPVRKVEVPVROBTDTUOBDEUPUQURUSUT
      $.
      $( [10-Apr-2004] $)
  $}

  ${
    opcom.1 $e |- A e. _V $.
    $( An ordered pair commutes iff its members are equal. $)
    opcom $p |- ( <. A , B >. = <. B , A >. <-> A = B ) $=
      ( cop wceq cvv opeqex mpbii eleq1 wa wb opthgg mp3an13 eqcom anbi2i anidm
      wcel bitri syl6bb pm5.21nii ) ABDBADEZBFQZABEZUAAFQZUBCABBAGHUCUDUBCABFIH
      UBUAUCBAEZJZUCUDUBUDUAUFKCCABBAFFFLMUFUCUCJUCUEUCUCBANOUCPRST $.
      $( [28-May-2009] $)
  $}

  ${
    $d x y z A $.  $d y z B $.
    $( "At most one" property of an ordered pair.  The proof is a little tricky
       because we do not place any restrictions on class ` B ` . $)
    moop2 $p |- E* x A = <. B , x >. $=
      ( vy vz cv cop wceq wmo csb wa wi wal eqtr2 vex opth2 syl gen2 wcel ax-17
      hbcsb1 hbop hbeq csbeq1a opeq1d opeq2 eqtrd eqeq2d mo4f mpbir ) BCAFZGZHZ
      AIUMBADFZCJZUNGZHZKZUKUNHZLZDMAMUTADURULUPHUSBULUPNCUKUOUNAODOPQRUMUQADAE
      EBUPEFZBSATAEUOUNAEUNCVAUNSATZUAVBUBUCUSULUPBUSULUOUKGUPUSCUOUKAUNCUDUEUK
      UNUOUFUGUHUIUJ $.
      $( [19-Dec-2008] $) $( [11-Apr-2004] $)
  $}

  ${
    opeqsn.1 $e |- A e. _V $.
    opeqsn.2 $e |- B e. _V $.
    opeqsn.3 $e |- C e. _V $.
    $( Equivalence for an ordered pair equal to a singleton. $)
    opeqsn $p |- ( <. A , B >. = { C } <-> ( A = B /\ C = { A } ) ) $=
      ( cop csn wceq wa df-op eqeq1i snex prex preqsn eqcom anbi2i bitri 3bitri
      cpr anidm anbi1i dfsn2 preq2 syl5req eqeq1d syl6bb pm5.32i ) ABGZCHZIAHZA
      BTZTZUJIUKULIZULCIZJZABIZCUKIZJZUIUMUJABKLUKULCAMABNFOUPUQUOJUSUNUQUOUNUL
      UKIUQBAIZJZUQUKULPABADEDOVAUQUQJUQUTUQUQBAPQUQUARSUBUQUOURUQUOUKCIURUQULU
      KCUQUKAATULAUCABAUDUEUFUKCPUGUHRS $.
      $( [3-Jun-2008] $)
  $}

  ${
    opeqpr.3 $e |- C e. _V $.
    opeqpr.4 $e |- D e. _V $.
    $( Equivalence for an ordered pair equal to an unordered pair. $)
    opeqpr $p |- ( <. A , B >. = { C , D }
  <-> ( ( C = { A } /\ D = { A , B } ) \/ ( C = { A , B } /\ D = { A } ) ) ) $=
      ( cop cpr wceq csn wa wo eqcom df-op eqeq2i snex prex preq12b 3bitri ) AB
      GZCDHZIUATIUAAJZABHZHZICUBIDUCIKCUCIDUBIKLTUAMTUDUAABNOCDUBUCEFAPABQRS $.
      $( [3-Jun-2008] $)
  $}

  ${
    $d x y z A $.
    $( "At most one" remains true inside ordered pair quantification. $)
    mosubopt $p |- ( A. y A. z E* x ph ->
                 E* x E. y E. z ( A = <. y , z >. /\ ph ) ) $=
      ( wmo wal cv cop wceq wex wa hba1 hbe1 hbmo wi hbex a4s exlimd wn copsexg
      ax-17 mobid biimpcd simpl 2eximi exlimiv con3i exmo ori syl pm2.61d1 ) AB
      FZDGZCGZECHDHIJZDKZCKZUPALZDKZCKZBFZUOUQVBCUNCMVACBUTCNOUNUQVBPCUNUPVBDUM
      DMVADBUTDCUSDNQOUMUPVBPDUPUMVBUPAVABUPBUBACDEUAUCUDRSRSURTVABKZTVBVCURVAU
      RBUSUPCDUPAUEUFUGUHVCVBVABUIUJUKUL $.
      $( [28-Aug-2007] $)
  $}

  ${
    $d x y z A $.
    mosubop.1 $e |- E* x ph $.
    $( "At most one" remains true inside ordered pair quantification. $)
    mosubop $p |- E* x E. y E. z ( A = <. y , z >. /\ ph ) $=
      ( wmo wal cv cop wceq wa wex gen2 mosubopt ax-mp ) ABGZDHCHECIDIJKALDMCMB
      GQCDFNABCDEOP $.
      $( [28-May-1995] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x y $.
    $( Transfer existential uniqueness to second member of an ordered pair. $)
    euop2 $p |- ( E! x E. y ( x = <. A , y >. /\ ph ) <-> E! y ph ) $=
      ( cv cop opex moop2 euxfr2 ) ABCDCEZFDJGCBEDHI $.
      $( [10-Apr-2004] $)
  $}

  ${
    opthw.1 $e |- A e. _V $.
    opthw.2 $e |- B e. _V $.
    $( Justification theorem for the ordered pair definition in Norbert Wiener,
       "A simplification of the logic of relations," _Proc. of the Cambridge
       Philos.  Soc_., 1914, vol. 17, pp.387-390.  It is also shown as a
       definition in [Enderton] p. 36 and as Exercise 4.8(b) of [Mendelson]
       p. 230.  It is meaningful only for classes that exist as sets (i.e. are
       not proper classes).  See ~ df-op for other ordered pair definitions. $)
    opthwiener $p |- ( { { { A } , (/) } , { { B } } } =
                    { { { C } , (/) } , { { D } } } <-> ( A = C /\ B = D ) ) $=
      ( csn c0 cpr wceq wcel snex prid2 wn 0ex eqcom syl sneqr 3syl sneq sylibr
      wa id wo eleq2 mpbii elpr sylib wb snnz elsnc bitri nemtbir nelneq2 mp2an
      mtbi biorf ax-mp preq2d eqtr4d prex preqr1 preq1 preq2 sylan9eq impbii
      jca ) AGZHIZBGZGZIZCGZHIZDGZGZIZJZACJZBDJZUBVRVSVTVRVHVMJZVSVRVIVNJZWAVRV
      LVNVKIZJZWBVRVLVQWCVRUCVRVKVPVNVRVKVNJZVKVPJZUDZWFVRVKVQKZWGVRVKVLKWHVIVK
      VJLZMVLVQVKUEUFVKVNVPWIUGUHWENWFWGUIVNVKJZWEHVNKHVKKZNWJNVMHOMWKVJHBFUJWK
      HVJJVJHJHVJOUKHVJPULUMHVNVKUNUOVNVKPUPWEWFUQURUAZUSUTVIVNVKVHHVAVMHVAVBQV
      HVMHALCLVBQACERQVRWFVJVOJZVTWLVJVOBLRBDFRSVGVSVTVLWCVQVSWAWBWDACTVHVMHVCV
      IVNVKVCSVTWMWFWCVQJBDTVJVOTVKVPVNVDSVEVF $.
      $( [28-Sep-2003] $)
  $}

  $( The union of an ordered pair.  Theorem 65 of [Suppes] p. 39. $)
  uniop $p |- U. <. A , B >. = { A , B } $=
    ( cop cuni csn cpr cun df-op unieqi snex prex wss wceq snsspr1 ssequn1 mpbi
    unipr 3eqtri ) ABCZDAEZABFZFZDTUAGZUASUBABHITUAAJABKQTUALUCUAMABNTUAOPR $.
    $( [17-Aug-2004] $)

  $( Ordered pair membership is inherited by class union. $)
  uniopel $p |- ( <. A , B >. e. C -> U. <. A , B >. e. U. C ) $=
    ( cop wcel cuni cpr uniop opi2 eqeltri elssuni sseld mpi ) ABDZCEZNFZNEPCFZ
    EPABGNABHABIJONQPNCKLM $.
    $( [13-May-2008] $)

