$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ordered-pair class abstractions (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x z w v $.  $d y z w v $.  $d ph z w v $.
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61.
       (The proof was shortened by Andrew Salmon, 25-Jul-2011.) $)
    opabid $p |- ( <. x , y >. e. { <. x , y >. | ph } <-> ph ) $=
      ( vz cv cop wceq wa wex copab opex copsexg bicomd df-opab elab2 ) DEZBEZC
      EZFZGZAHCIBIZADSABCJQRKTAUAABCPLMABCDNO $.
      $( [25-Jul-2011] $) $( [14-Apr-1995] $)
  $}

  ${
    $d x z A $.  $d y z A $.  $d z ph $.
    $( Membership in a class abstraction of pairs. $)
    elopab $p |- ( A e. { <. x , y >. | ph } <->
                 E. x E. y ( A = <. x , y >. /\ ph ) ) $=
      ( vz copab wcel cvv cv cop wceq wa elex opex eleq1 mpbiri adantr exlimivv
      wex eqeq1 anbi1d 2exbidv df-opab abeq2i vtoclbg pm5.21nii ) DABCFZGZDHGZD
      BIZCIZJZKZALZCSBSZDUGMUNUIBCUMUIAUMUIULHGUJUKNDULHOPQREIZUGGUPULKZALZCSBS
      ZUHUOEDHUPDUGOUPDKZURUNBCUTUQUMAUPDULTUAUBUSEUGABCEUCUDUEUF $.
      $( [24-Mar-1998] $)
  $}

  ${
    $d x z v $.  $d y z v $.  $d w z $.  $d ph v $.
    hbopab.1 $e |- ( ph -> A. z ph ) $.
    $( Bound-variable hypothesis builder for class abstraction.  (Unnecessary
       distinct variable restrictions were removed by Andrew Salmon,
       11-Jul-2011.) $)
    hbopab $p |- ( w e. { <. x , y >. | ph } ->
                 A. z w e. { <. x , y >. | ph } ) $=
      ( vv copab cv cop wceq wa wex cab df-opab ax-17 hban hbex hbab hbxfreq )
      DEABCHGIBICIJKZALZCMZBMZGNABCGOUDDGEUCDBUBDCUAADUADPFQRRST $.
      $( [25-Jul-2011] $) $( [1-Sep-1999] $)
  $}

  ${
    $d x w $.  $d y w $.  $d x z $.  $d w ph $.
    $( The first abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab1 $p |- ( z e. { <. x , y >. | ph } ->
                 A. x z e. { <. x , y >. | ph } ) $=
      ( vw copab cv cop wceq wa wex cab df-opab hbe1 hbab hbxfreq ) BDABCFEGBGC
      GHIAJCKZBKZELABCEMRBEDQBNOP $.
      $( [16-May-1995] $)
  $}

  ${
    $d x w $.  $d y w $.  $d y z $.  $d w ph $.
    $( The second abstraction variable in an ordered-pair class abstraction
       (class builder) is effectively not free. $)
    hbopab2 $p |- ( z e. { <. x , y >. | ph } ->
                 A. y z e. { <. x , y >. | ph } ) $=
      ( vw copab cv cop wceq wa wex cab df-opab hbe1 hbex hbab hbxfreq ) CDABCF
      EGBGCGHIAJZCKZBKZELABCEMTCEDSCBRCNOPQ $.
      $( [16-May-1995] $)
  $}


  ${
    $d x y z v $.  $d x y w v $.  $d v ph $.
    $( The law of concretion in terms of substitutions.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    opelopabsb $p |- ( <. z , w >. e. { <. x , y >. | ph }
                 <-> [ w / y ] [ z / x ] ph ) $=
      ( cv cop wceq wa wex copab wcel wsbc excom vex opth equcom anbi12i 3bitri
      ancom anbi1i 2exbii bitri elopab 2sb5 3bitr4i ) DFZEFZGZBFZCFZGHZAIZCJBJZ
      UKUHHZUJUGHZIZAIZBJCJZUIABCKLABUGMCUHMUNUMBJCJUSUMBCNUMURCBULUQAULUGUJHZU
      HUKHZIVAUTIUQUGUHUJUKDOEOCOPUTVATVAUOUTUPECQDBQRSUAUBUCABCUIUDACBEDUEUF
      $.
      $( [25-Jul-2011] $) $( [30-Sep-2002] $)

    ${
      brabsb.1 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion in terms of substitutions. $)
      brabsb $p |- ( z R w <-> [ w / y ] [ z / x ] ph ) $=
        ( cv wbr copab cop wcel wsbc breqi df-br opelopabsb 3bitri ) DHZEHZFIRS
        ABCJZIRSKTLABRMCSMRSFTGNRSTOABCDEPQ $.
        $( [17-Mar-2008] $)
    $}
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y ch $.  $d z ph $.
    $( Closed theorem form of ~ opelopab . $)
    opelopabt $p |- ( ( A. x A. y ( x = A -> ( ph <-> ps ) )
                     /\ A. x A. y ( y = B -> ( ps <-> ch ) )
                     /\ ( A e. V /\ B e. W ) ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) ) $=
      ( cop copab wcel cv wceq wa wex wb wi wal w3a elopab prth 2alimi copsex2t
      19.26-2 bitr syl6 sylbir sylan 3impa syl5bb ) FGJZADEKLULDMZEMZJNAOEPDPZU
      MFNZABQZRZESDSZUNGNZBCQZRZESDSZFHLGILOZTCADEULUAUSVCVDUOCQZUSVCOZUPUTOZAC
      QZRZESDSZVDVEVFURVBOZESDSVJURVBDEUEVKVIDEVKVGUQVAOVHUPUQUTVAUBABCUFUGUCUH
      ACDEFGHIUDUIUJUK $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    opelopabga.1 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61.  (Contributed by
       Mario Carneiro, 19-Dec-2013.) $)
    opelopabga $p |- ( ( A e. V /\ B e. W ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ps ) ) $=
      ( cop copab wcel cv wceq wa wex elopab copsex2g syl5bb ) EFJZACDKLTCMDMJN
      AODPCPEGLFHLOBACDTQABCDEFGHIRS $.
      $( [19-Dec-2013] $)

    ${
      brabga.2 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation.  (Contributed by Mario
         Carneiro, 19-Dec-2013.) $)
      brabga $p |- ( ( A e. V /\ B e. W ) -> ( A R B <-> ps ) ) $=
        ( wbr cop copab wcel wa df-br eleq2i bitri opelopabga syl5bb ) EFGLZEFM
        ZACDNZOZEHOFIOPBUBUCGOUEEFGQGUDUCKRSABCDEFHIJTUA $.
        $( [19-Dec-2013] $)
    $}

    $d x y C $.  $d x y D $.
    $( Ordered pair membership in an ordered pair class abstraction.
       (Contributed by Mario Carneiro, 19-Dec-2013.) $)
    opelopab2a $p |- ( ( A e. C /\ B e. D ) -> ( <. A , B >. e.
                 { <. x , y >. | ( ( x e. C /\ y e. D ) /\ ph ) } <-> ps ) ) $=
      ( wcel wa cop cv copab wceq eleq1 bi2anan9 anbi12d opelopabga ibar bitr4d
      ) EGJZFHJZKZEFLCMZGJZDMZHJZKZAKZCDNJUDBKZBUJUKCDEFGHUEEOZUGFOZKUIUDABULUF
      UBUMUHUCUEEGPUGFHPQIRSUDBTUA $.
      $( [19-Dec-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ps $.
    opelopaba.1 $e |- A e. _V $.
    opelopaba.2 $e |- B e. _V $.
    opelopaba.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61.  (Contributed by
       Mario Carneiro, 19-Dec-2013.) $)
    opelopaba $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ps ) $=
      ( cvv wcel cop copab wb opelopabga mp2an ) EJKFJKEFLACDMKBNGHABCDEFJJIOP
      $.
      $( [19-Dec-2013] $)

    ${
      braba.4 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      braba $p |- ( A R B <-> ps ) $=
        ( cvv wcel wbr wb brabga mp2an ) ELMFLMEFGNBOHIABCDEFGLLJKPQ $.
        $( [19-Dec-2013] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.
    opelopabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopabg $p |- ( ( A e. V /\ B e. W ) ->
                    ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) ) $=
      ( cv wceq sylan9bb opelopabga ) ACDEFGHIDLFMABELGMCJKNO $.
      $( [19-Dec-2013] $) $( [28-May-1995] $)

    ${
      brabg.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brabg $p |- ( ( A e. C /\ B e. D ) -> ( A R B <-> ch ) ) $=
        ( cv wceq sylan9bb brabga ) ACDEFGJHIDNFOABENGOCKLPMQ $.
        $( [19-Dec-2013] $) $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y D $.  $d x y ch $.
    opelopab2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab2.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( Ordered pair membership in an ordered pair class abstraction. $)
    opelopab2 $p |- ( ( A e. C /\ B e. D ) -> ( <. A , B >. e.
                 { <. x , y >. | ( ( x e. C /\ y e. D ) /\ ph ) } <-> ch ) ) $=
      ( cv wceq sylan9bb opelopab2a ) ACDEFGHIDLFMABELGMCJKNO $.
      $( [19-Dec-2013] $) $( [14-Oct-2007] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y ch $.
    opelopab.1 $e |- A e. _V $.
    opelopab.2 $e |- B e. _V $.
    opelopab.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopab.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61. $)
    opelopab $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( cvv wcel cop copab wb opelopabg mp2an ) FLMGLMFGNADEOMCPHIABCDEFGLLJKQR
      $.
      $( [16-May-1995] $)

    ${
      brab.5 $e |- R = { <. x , y >. | ph } $.
      $( The law of concretion for a binary relation. $)
      brab $p |- ( A R B <-> ch ) $=
        ( cvv wcel wbr wb brabg mp2an ) FNOGNOFGHPCQIJABCDEFGNNHKLMRS $.
        $( [16-Aug-1999] $)
    $}
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w z ph $.  $d w z ps $.
    opelopabaf.x $e |- ( ps -> A. x ps ) $.
    opelopabaf.y $e |- ( ps -> A. y ps ) $.
    opelopabaf.1 $e |- A e. _V $.
    opelopabaf.2 $e |- B e. _V $.
    opelopabaf.3 $e |- ( ( x = A /\ y = B ) -> ( ph <-> ps ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61.  This version of
       ~ opelopab uses bound-variable hypotheses in place of distinct variable
       conditions."  (Contributed by Mario Carneiro, 19-Dec-2013.) $)
    opelopabaf $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ps ) $=
      ( vw vz copab wcel cv wsbc ax-17 wceq cvv cop hbs1 hbsb sbequ12 sylan9bbr
      cbvopab eleq2i wa dfsbcq sbbid wb sbc2iegf mp2an syl6bb opelopaba bitri )
      EFUAZACDNZOUQADLPZQZCMPZQZMLNZOBURVCUQAVBCDMLAMRALRUTCMUBUTCMDADLUBUCDPUS
      SAUTCPVASVBADLUDUTCMUDUEUFUGVBBMLEFIJVAESZUSFSZUHVBADFQZCEQZBVEVBVFCVAQVD
      VGVEUTVFCMVECRADUSFUIUJVFCVAEUIUEETOFTOZVGBUKIJABCDEFTTGHVHCRKULUMUNUOUP
      $.
      $( [19-Dec-2013] $)
  $}

  ${
    $d w x y z A $.  $d w y z B $.  $d w z ch $.  $d w z ph $.
    opelopabf.x $e |- ( ps -> A. x ps ) $.
    opelopabf.y $e |- ( ch -> A. y ch ) $.
    opelopabf.1 $e |- A e. _V $.
    opelopabf.2 $e |- B e. _V $.
    opelopabf.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    opelopabf.4 $e |- ( y = B -> ( ps <-> ch ) ) $.
    $( The law of concretion.  Theorem 9.5 of [Quine] p. 61.  This version of
       ~ opelopab uses bound-variable hypotheses in place of distinct variable
       conditions." $)
    opelopabf $p |- ( <. A , B >. e. { <. x , y >. | ph } <-> ch ) $=
      ( vz vw copab cv wsbc ax-17 wceq cop wcel sbequ12 sylan9bb cbvopab eleq2i
      hbs1 hbsb sbhypf sbbid opelopab bitri ) FGUAZADEPZUBUMADNQZRZEOQZRZNOPZUB
      CUNUSUMAURDENOANSAOSUPEODADNUGUHUPEOUGDQUOTAUPEQUQTURADNUCUPEOUCUDUEUFURB
      EUQRCNOFGJKUOFTZUPBEOUTESABDNFHLUIUJBCEOGIMUIUKUL $.
      $( [19-Dec-2008] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x z $.  $d y z $.
    $( Equivalence of ordered pair abstraction subclass and implication.
       (Revised by Mario Carneiro, 19-May-2013.) $)
    ssopab2 $p |- ( A. x A. y ( ph -> ps ) ->
        { <. x , y >. | ph } C_ { <. x , y >. | ps } ) $=
      ( vz wi wal cv cop wceq wex cab copab hba1 ax-4 anim2d eximd a4s df-opab
      wa ss2abdv 3sstr4g ) ABFZDGZCGZEHCHDHIJZATZDKZCKZELUFBTZDKZCKZELACDMBCDMU
      EUIULEUEUHUKCUDCNUDUHUKFCUDUGUJDUCDNUDABUFUCDOPQRQUAACDESBCDESUB $.
      $( [19-May-2013] $) $( [27-Dec-1996] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x z $.  $d y z $.
    $( Equivalence of ordered pair abstraction subclass and implication. $)
    ssopab2b $p |- ( { <. x , y >. | ph } C_ { <. x , y >. | ps } <->
               A. x A. y ( ph -> ps ) ) $=
      ( vz copab wss wi wal hbopab1 hbss hbopab2 cv cop wex cab df-opab copsexg
      wa alrimi wceq opex isseti sseq12i ss2ab ax-4 sylbi imbi12d syl5ibr ax-mp
      syl5bi exlimiv ssopab2 impbii ) ACDFZBCDFZGZABHZDIZCIUQUSCCEUOUPACDEJBCDE
      JKUQURDDEUOUPACDELBCDELKEMZCMZDMZNZUAZEOUQURHZEVCVAVBUBUCVDVEEUQVDASDOCOZ
      EPZVDBSDOCOZEPZGZVDURUOVGUPVIACDEQBCDEQUDVJURVDVFVHHZVJVKEIVKVFVHEUEVKEUF
      UGVDAVFBVHACDUTRBCDUTRUHUIUKULUJTTABCDUMUN $.
      $( [27-Dec-1996] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x z $.  $d y z $.
    $( Equivalence of ordered pair abstraction subclass and implication. $)
    ssopab2OLD $p |- ( { <. x , y >. | ph } C_ { <. x , y >. | ps } <->
               A. x A. y ( ph -> ps ) ) $=
      ( vz copab wss wi wal hbopab1 hbss hbopab2 cv wex wa df-opab ax-4 copsexg
      cab alrimi wceq isseti sseq12i ss2ab sylbi imbi12d syl5ibr syl5bi exlimiv
      cop opex ax-mp hba1 anim2d eximd a4s ss2abdv 3sstr4g impbii ) ACDFZBCDFZG
      ZABHZDIZCIZVBVDCCEUTVAACDEJBCDEJKVBVCDDEUTVAACDELBCDELKEMZCMZDMZUJZUAZENV
      BVCHZEVIVGVHUKUBVJVKEVBVJAOZDNZCNZESZVJBOZDNZCNZESZGZVJVCUTVOVAVSACDEPZBC
      DEPZUCVTVCVJVNVRHZVTWCEIWCVNVREUDWCEQUEVJAVNBVRACDVFRBCDVFRUFUGUHUIULTTVE
      VOVSUTVAVEVNVREVEVMVQCVDCUMVDVMVQHCVDVLVPDVCDUMVDABVJVCDQUNUOUPUOUQWAWBUR
      US $.
      $( [27-Aug-2011] $) $( [27-Dec-1996] $)
  $}

  ${
    ssopab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of ordered pair abstraction subclass from implication. $)
    ssopab2i $p |- { <. x , y >. | ph } C_ { <. x , y >. | ps } $=
      ( wi wal copab wss ssopab2 ax-gen mpg ) ABFZDGACDHBCDHICABCDJMDEKL $.
      $( [5-Apr-1995] $)
  $}

  ${
    $d x ph $.  $d y ph $.
    ssopab2dv.1 $e |- ( ph -> ( ps -> ch ) ) $.
    $( Inference of ordered pair abstraction subclass from implication. $)
    ssopab2dv $p |- ( ph -> { <. x , y >. | ps } C_ { <. x , y >. | ch } ) $=
      ( wi wal copab wss alrimivv ssopab2b sylibr ) ABCGZEHDHBDEICDEIJANDEFKBCD
      ELM $.
      $( [19-Jan-2014] $)
  $}

  ${
    $d z ph $.  $d z x $.  $d z y $.
    $( Non-empty ordered pair class abstraction. $)
    opabn0 $p |- ( { <. x , y >. | ph } =/= (/) <-> E. x E. y ph ) $=
      ( vz copab c0 wne cv wcel wex cop wceq wa elopab exbii exrot3 19.41v opex
      n0 isseti mpbiran 2exbii bitri 3bitri ) ABCEZFGDHZUEIZDJUFBHZCHZKZLZAMZCJ
      BJZDJZACJBJZDUESUGUMDABCUFNOUNULDJZCJBJUOULDBCPUPABCUPUKDJAUKADQDUJUHUIRT
      UAUBUCUD $.
      $( [10-Oct-2007] $)
  $}

