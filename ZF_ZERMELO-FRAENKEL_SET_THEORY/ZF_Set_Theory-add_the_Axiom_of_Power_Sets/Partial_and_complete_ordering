$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Partial and complete ordering
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

$(
We have not yet defined relations ( ~ df-rel ), but here but introduce a
few related notions we will use to develop ordinals.  The class variable
` R ` is no different from other class variables, but it reminds us that
normally it represents what we will later call a "relation."
$)

  $( Declare new constant symbols. $)
  $c Po $.  $( Partial ordering predicate symbol (read: 'partial ordering'). $)
  $c Or $.  $( Strict complete ordering predicate symbol (read: 'orders'). $)

  $( Extend wff notation to include the strict partial ordering predicate.
     Read:  ' ` R ` is a partial order on ` A ` .' $)
  wpo $a wff R Po A $.

  $( Extend wff notation to include the strict complete ordering predicate.
     Read:  ' ` R ` orders ` A ` .' $)
  wor $a wff R Or A $.

  ${
    $d x y z R $.  $d x y z A $.
    $( Define the strict partial order predicate.  Definition of [Enderton]
       p. 168. $)
    df-po $a |- ( R Po A <-> A. x e. A A. y e. A A. z e. A
                ( -. x R x /\ ( ( x R y /\ y R z ) -> x R z ) ) ) $.
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Subset theorem for the partial ordering predicate.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    poss $p |- ( A C_ B -> ( R Po B -> R Po A ) ) $=
      ( vx vy vz wss cv wbr wa wi wral wpo wcel w3a wal ssel r3al 3imtr4g df-po
      wn 3anim123d imim1d alimdv 2alimdv ) ABGZDHZUGCIUAUGEHZCIUHFHZCIJUGUICIKJ
      ZFBLEBLDBLZUJFALEALDALZBCMACMUFUGBNZUHBNZUIBNZOZUJKZFPZEPDPUGANZUHANZUIAN
      ZOZUJKZFPZEPDPUKULUFURVDDEUFUQVCFUFVBUPUJUFUSUMUTUNVAUOABUGQABUHQABUIQUBU
      CUDUEUJDEFBBBRUJDEFAAARSDEFBCTDEFACTS $.
      $( [25-Jul-2011] $) $( [27-Mar-1997] $)
  $}

  ${
    $d x y z R $.  $d x y z S $.  $d x y z A $.
    $( Equality theorem for partial ordering predicate. $)
    poeq1 $p |- ( R = S -> ( R Po A <-> S Po A ) ) $=
      ( vx vy vz wceq cv wbr wn wa wral wpo breq notbid anbi12d imbi12d ralbidv
      wi df-po 2ralbidv 3bitr4g ) BCGZDHZUDBIZJZUDEHZBIZUGFHZBIZKZUDUIBIZSZKZFA
      LZEALDALUDUDCIZJZUDUGCIZUGUICIZKZUDUICIZSZKZFALZEALDALABMACMUCUOVDDEAAUCU
      NVCFAUCUFUQUMVBUCUEUPUDUDBCNOUCUKUTULVAUCUHURUJUSUDUGBCNUGUIBCNPUDUIBCNQP
      RUADEFABTDEFACTUB $.
      $( [27-Mar-1997] $)
  $}

  $( Equality theorem for partial ordering predicate. $)
  poeq2 $p |- ( A = B -> ( R Po A <-> R Po B ) ) $=
    ( wceq wpo wss wa wi wb poss anim12i eqss dfbi2 3imtr4i bicomd ) ABDZBCEZAC
    EZABFZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [27-Mar-1997] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.
    $( Properties of partial order relation in class notation. $)
    pocl $p |- ( R Po A -> ( ( B e. A /\ C e. A /\ D e. A ) ->
               ( -. B R B /\ ( ( B R C /\ C R D ) -> B R D ) ) ) ) $=
      ( vx vy vz wcel wbr wa wi cv wceq breq1 imbi2d breq2 anbi2d wal wral r3al
      w3a wn id breq12d notbid anbi1d imbi12d anbi12d imbi1d df-po bitri biimpi
      wpo 19.21bbi 19.21bi com12 vtocl3ga ) BAICAIDAIUBAEUNZBBEJZUCZBCEJZCDEJZK
      ZBDEJZLZKZUSFMZVHEJZUCZVHGMZEJZVKHMZEJZKZVHVMEJZLZKZLUSVABVKEJZVNKZBVMEJZ
      LZKZLUSVAVBCVMEJZKZWALZKZLUSVGLFGHBCDAAAVHBNZVRWCUSWHVJVAVQWBWHVIUTWHVHBV
      HBEWHUDZWIUEUFWHVOVTVPWAWHVLVSVNVHBVKEOUGVHBVMEOUHUIPVKCNZWCWGUSWJWBWFVAW
      JVTWEWAWJVSVBVNWDVKCBEQVKCVMEOUIUJRPVMDNZWGVGUSWKWFVFVAWKWEVDWAVEWKWDVCVB
      VMDCEQRVMDBEQUHRPUSVHAIVKAIVMAIUBZVRUSWLVRLZHUSWMHSZFGUSWNGSFSZUSVRHATGAT
      FATWOFGHAEUKVRFGHAAAUAULUMUOUPUQURUQ $.
      $( [27-Mar-1997] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.  $d x y z ph $.
    ispod.1 $e |- ( ( ph /\ x e. A ) -> -. x R x ) $.
    ispod.2 $e |- ( ( ph /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                    ( ( x R y /\ y R z ) -> x R z ) ) $.
    $( Sufficient conditions for a partial order. $)
    ispod $p |- ( ph -> R Po A ) $=
      ( cv wbr wn wa wi wral wpo wcel w3a 3ad2antr1 jca ralrimivvva sylibr
      df-po ) ABIZUCFJKZUCCIZFJUEDIZFJLUCUFFJMZLZDENCENBENEFOAUHBCDEEEAUCEPZUEE
      PZUFEPZQLUDUGAUJUIUDUKGRHSTBCDEFUBUA $.
      $( [9-Jul-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.  $d x y z ph $.
    swopo.1 $e |- ( ( ph /\ ( y e. A /\ z e. A ) ) ->
                    ( y R z -> -. z R y ) ) $.
    swopo.2 $e |- ( ( ph /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                    ( x R y -> ( x R z \/ z R y ) ) ) $.
    $( A strict weak order is a partial order. $)
    swopo $p |- ( ph -> R Po A ) $=
      ( cv wcel wa wbr wn wi wral wceq breq1 breq2 notbid imbi12d id ralrimivva
      ancli rcla42va syl2anr pm2.01d w3a 3adantr1 wo imp orcomd expimpd sylan2d
      ord ispod ) ABCDEFABIZEJZKUPUPFLZUQUQUQKCIZDIZFLZUTUSFLZMZNZDEOCEOURURMZN
      ZAUQUQUQUAUCAVDCDEEGUBVDVFUPUTFLZUTUPFLZMZNCDUPUPEEUSUPPZVAVGVCVIUSUPUTFQ
      VJVBVHUSUPUTFRSTUTUPPZVGURVIVEUTUPUPFRVKVHURUTUPUPFQSTUDUEUFAUQUSEJZUTEJZ
      UGKZVAVCUPUSFLZVGAVLVMVDUQGUHVNVOVCVGVNVOKZVBVGVPVGVBVNVOVGVBUIHUJUKUNULU
      MUO $.
      $( [9-Jul-2014] $)
  $}

  $( A partial order relation is irreflexive. $)
  poirr $p |- ( ( R Po A /\ B e. A ) -> -. B R B ) $=
    ( wcel wpo w3a wbr wn wa df-3an anabs1 anidm 3bitrri wi pocl simpld sylan2b
    imp ) BADZACEZSSSFZBBCGZHZUASSIZSIUDSSSSJSSKSLMTUAIUCUBUBIUBNZTUAUCUEIABBBC
    ORPQ $.
    $( [27-Mar-1997] $)

  $( A partial order relation is a transitive relation. $)
  potr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wpo wcel w3a wa wbr wn wi pocl imp simprd ) AEFZBAGCAGDAGHZIBBEJKZBCEJCDE
    JIBDEJLZPQRSIABCDEMNO $.
    $( [27-Mar-1997] $)

  $( A partial order relation has no 2-cycle loops. $)
  po2nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wpo wcel wa wbr wn poirr adantrr wi potr 3exp2 com34 pm2.43d imp32 mtod )
    ADEZBAFZCAFZGGBCDHCBDHGZBBDHZSTUCIUAABDJKSTUAUBUCLZSTUAUDLSTUATUDSTUATUDABC
    BDMNOPQR $.
    $( [27-Mar-1997] $)

  $( A partial order relation has no 3-cycle loops. $)
  po3nr $p |- ( ( R Po A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wpo wcel w3a wa wbr wn po2nr 3adantr2 df-3an potr anim1d syl5bi mtod ) AE
    FZBAGZCAGZDAGZHIZBCEJZCDEJZDBEJZHZBDEJZUFIZSTUBUIKUAABDELMUGUDUEIZUFIUCUIUD
    UEUFNUCUJUHUFABCDEOPQR $.
    $( [27-Mar-1997] $)

  ${
    $d x y z R $.
    $( Any relation is a partial ordering of the empty set.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    po0 $p |- R Po (/) $=
      ( vx vy vz c0 wpo cv wbr wn wa wi wral ral0 df-po mpbir ) EAFBGZPAHIPCGZA
      HQDGZAHJPRAHKJDELCELZBELSBMBCDEANO $.
      $( [25-Jul-2011] $) $( [28-Mar-1997] $)
  $}

  ${
    $d w y z R $.  $d w x y z $.
    $( MAY BE REVISED - use A instead of x $)
    $( Partial ordering of a singleton. $)
    posn $p |- ( R Po { x } <-> -. <. x , x >. e. R ) $=
      ( vy vz vw cv wbr wn wa wral wsb ralsn ax-17 weq breq1 breq2 imbi12d sbie
      wi anbi2d csn wpo cop wcel df-po bitrd notbid anbi1d anbi12d bitri imbi1d
      vex sbbii 3bitri simpl biantru 3bitr4ri 3bitr4i df-br xchbinx ) AFZUAZBUB
      ZVAVABGZVAVAUCBUDCFZVEBGZHZVEDFZBGZVHEFZBGZIZVEVJBGZSZIZEVBJZDVBJZCVBJVQC
      AKZVCVDHZVQCVAAULZLCDEVBBUEVGVEVABGZVDIZWASZIZCAKVSVDVDIZVDSZIZVRVSWDWGCA
      WGCMCANZVGVSWCWFWHVFVDWHVFVAVEBGVDVEVAVEBOVEVAVABPUFUGWHWBWEWAVDWHWAVDVDV
      EVAVABOZUHWIQUIRVQWDCAVQVPDAKVGVIVHVABGZIZWASZIZDAKWDVPDVAVTLVPWMDAVPVOEA
      KWMVOEVAVTLVOWMEAWMEMEANZVNWLVGWNVLWKVMWAWNVKWJVIVJVAVHBPTVJVAVEBPQTRUJUM
      WMWDDAWDDMDANZWLWCVGWOWKWBWAWOVIWAWJVDVHVAVEBPVHVAVABOUIUKTRUNUMWFVSVDVDU
      OUPUQURVAVABUSUT $.
      $( [27-Apr-2009] $)
  $}

  ${
    $d R v w x y z $.  $d S v w z $.  $d X v w y z $.  $d Y x z $.
    $d A v w x z $.  $d B v w x z $.
    pofun.1 $e |- S = { <. x , y >. | X R Y } $.
    pofun.2 $e |- ( x = y -> X = Y ) $.
    $( A function preserves a partial order relation.  (Contributed by Jeff
       Madsen, 18-Jun-2011.) $)
    pofun $p |- ( ( R Po B /\ A. x e. A X e. B ) -> S Po A ) $=
      ( vw vz wcel wa cv wbr csb vex ax-17 wceq vv wpo wral hbcsb1 hbel csbeq1a
      wn eleq1d rcla4 impcom poirr copab df-br eleq2i hbbr breq1d csbief csbeq1
      syl5eqr breq2d opelopabf 3bitri sylnibr sylan2 anassrs wi com12 3anim123d
      cop w3a imp adantll potr anbi12i 3imtr4g adantlr syldan ispod ) DEUBZGDMZ
      ACUCZNZUAKLCFVSWAUAOZCMZWCWCFPZUGZWAWDNVSAWCGQZDMZWFWDWAWHVTWHAWCCALLWGDA
      LWCGUARZLOZWCMASUDZWJDMASZUEAOZWCTZGWGDAWCGUFZUHUIZUJVSWHNWGWGEPZWEDWGEUK
      WEWCWCVIZFMWRGHEPZABULZMWQWCWCFUMFWTWRIUNWSWGHEPZWQABWCWCALWGHEWKWJEMASZW
      JHMASZUOZWQBSWIWIWNGWGHEWOUPZBOZWCTZHWGWGEXGHAXFGQZWGALXFGHBRXCJUQZAXFWCG
      URUSUTVAVBVCVDVEWBWDKOZCMZWJCMZVJZWHAXJGQZDMZAWJGQZDMZVJZWCXJFPZXJWJFPZNZ
      WCWJFPZVFZWAXMXRVSWAXMXRWAWDWHXKXOXLXQWDWAWHWPVGXKWAXOVTXOAXJCALLXNDALXJG
      KRZWJXJMASUDZWLUEWMXJTZGXNDAXJGUFZUHUIVGXLWAXQVTXQAWJCAKLXPDAKWJGLRZXJWJM
      ASUDWLUEWMWJTGXPDAWJGUFUHUIVGVHVKVLVSXRYCWAVSXRNWGXNEPZXNXPEPZNWGXPEPZYAY
      BDWGXNXPEVMXSYIXTYJXSWCXJVIZFMYLWTMYIWCXJFUMFWTYLIUNWSXAYIABWCXJXDYIBSWIY
      DXEXFXJTZHXNWGEYMHXHXNXIAXFXJGURUSUTVAVBXTXJWJVIZFMYNWTMYJXJWJFUMFWTYNIUN
      WSXNHEPYJABXJWJALXNHEYEXBXCUOYJBSYDYHYFGXNHEYGUPXFWJTZHXPXNEYOHXHXPXIAXFW
      JGURUSZUTVAVBVNYBWCWJVIZFMYQWTMYKWCWJFUMFWTYQIUNWSXAYKABWCWJXDYKBSWIYHXEY
      OHXPWGEYPUTVAVBVOVPVQVR $.
      $( [18-Jun-2010] $)
  $}

  ${
    $d x y R $.  $d x y A $.
    $( Define the strict complete (linear) order predicate. $)
    df-so $a |- ( R Or A <-> ( R Po A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $.
  $}

  ${
    $d x y R $.  $d x y A $.
    $( A strict linear order is a strict partial order. $)
    sopo $p |- ( R Or A -> R Po A ) $=
      ( vx vy wor wpo cv wbr wceq w3o wral df-so simplbi ) ABEABFCGZDGZBHNOIONB
      HJDAKCAKCDABLM $.
      $( [28-Mar-1997] $)
  $}

  ${
    $d x y R $.  $d x y A $.  $d x y B $.
    $( Subset theorem for the strict ordering predicate.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    soss $p |- ( A C_ B -> ( R Or B -> R Or A ) ) $=
      ( vx vy wss wpo cv wbr wral wa wor wcel wi wal ssel anim12d 3imtr4g df-so
      r2al wceq w3o poss imim1d 2alimdv ) ABFZBCGZDHZEHZCIUHUIUAUIUHCIUBZEBJDBJ
      ZKACGZUJEAJDAJZKBCLACLUFUGULUKUMABCUCUFUHBMZUIBMZKZUJNZEODOUHAMZUIAMZKZUJ
      NZEODOUKUMUFUQVADEUFUTUPUJUFURUNUSUOABUHPABUIPQUDUEUJDEBBTUJDEAATRQDEBCSD
      EACSR $.
      $( [25-Jul-2011] $) $( [16-Mar-1997] $)
  $}

  ${
    $d x y R $.  $d x y S $.  $d x y A $.
    $( Equality theorem for the strict ordering predicate. $)
    soeq1 $p |- ( R = S -> ( R Or A <-> S Or A ) ) $=
      ( vx vy wceq wpo cv wbr w3o wral wa wor poeq1 breq biidd 2ralbidv anbi12d
      3orbi123d df-so 3bitr4g ) BCFZABGZDHZEHZBIZUDUEFZUEUDBIZJZEAKDAKZLACGZUDU
      ECIZUGUEUDCIZJZEAKDAKZLABMACMUBUCUKUJUOABCNUBUIUNDEAAUBUFULUGUGUHUMUDUEBC
      OUBUGPUEUDBCOSQRDEABTDEACTUA $.
      $( [16-Mar-1997] $)
  $}

  $( Equality theorem for the strict ordering predicate. $)
  soeq2 $p |- ( A = B -> ( R Or A <-> R Or B ) ) $=
    ( wceq wor wss wa wi wb soss anim12i eqss dfbi2 3imtr4i bicomd ) ABDZBCEZAC
    EZABFZBAFZGQRHZRQHZGPQRISUATUBABCJBACJKABLQRMNO $.
    $( [16-Mar-1997] $)

  $( A strict order relation is irreflexive. $)
  sonr $p |- ( ( R Or A /\ B e. A ) -> -. B R B ) $=
    ( wor wpo wcel wbr wn sopo poirr sylan ) ACDACEBAFBBCGHACIABCJK $.
    $( [24-Nov-1995] $)

  $( A strict order relation is a transitive relation. $)
  sotr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             ( ( B R C /\ C R D ) -> B R D ) ) $=
    ( wor wpo wcel w3a wbr wa wi sopo potr sylan ) AEFAEGBAHCAHDAHIBCEJCDEJKBDE
    JLAEMABCDENO $.
    $( [21-Jan-1996] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.  $d x y R $.
    $( A strict order relation is linear (satisfies trichotomy). $)
    solin $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C \/ B = C \/ C R B ) ) $=
      ( vx vy wcel wa wor wbr wceq w3o cv wi breq1 eqeq1 breq2 3orbi123d imbi2d
      wral eqeq2 wpo df-so ra42 adantl sylbi com12 vtocl2ga impcom ) BAGCAGHADI
      ZBCDJZBCKZCBDJZLZUJEMZFMZDJZUOUPKZUPUODJZLZNUJBUPDJZBUPKZUPBDJZLZNUJUNNEF
      BCAAUOBKZUTVDUJVEUQVAURVBUSVCUOBUPDOUOBUPPUOBUPDQRSUPCKZVDUNUJVFVAUKVBULV
      CUMUPCBDQUPCBUAUPCBDORSUJUOAGUPAGHZUTUJADUBZUTFATEATZHVGUTNZEFADUCVIVJVHU
      TEFAAUDUEUFUGUHUI $.
      $( [21-Jan-1996] $)
  $}

  $( A strict order relation has no 2-cycle loops. $)
  so2nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              -. ( B R C /\ C R B ) ) $=
    ( wor wpo wcel wa wbr wn sopo po2nr sylan ) ADEADFBAGCAGHBCDICBDIHJADKABCDL
    M $.
    $( [21-Jan-1996] $)

  $( A strict order relation has no 3-cycle loops. $)
  so3nr $p |- ( ( R Or A /\ ( B e. A /\ C e. A /\ D e. A ) ) ->
             -. ( B R C /\ C R D /\ D R B ) ) $=
    ( wor wpo wcel w3a wbr wn sopo po3nr sylan ) AEFAEGBAHCAHDAHIBCEJCDEJDBEJIK
    AELABCDEMN $.
    $( [21-Jan-1996] $)

  $( A strict order relation satisfies strict trichotomy. $)
  sotric $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B R C <-> -. ( B = C \/ C R B ) ) ) $=
    ( wor wcel wa wceq wo wn wi sonr breq2 notbid syl5ibcom adantrr so2nr imnan
    wbr sylibr con2d jaod w3o solin 3orass df-or bitri sylib impbid con2bid ) A
    DEZBAFZCAFZGGZBCHZCBDSZIZBCDSZUNUQURJZUNUOUSUPUKULUOUSKUMUKULGBBDSZJUOUSABD
    LUOUTURBCBDMNOPUNURUPUNURUPGJURUPJKABCDQURUPRTUAUBUNURUOUPUCZUSUQKZABCDUDVA
    URUQIVBURUOUPUEURUQUFUGUHUIUJ $.
    $( [19-Feb-1996] $)

  $( Trichotomy law for strict order relation.  (The proof was shortened by
     Andrew Salmon, 25-Jul-2011.) $)
  sotrieq $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> -. ( B R C \/ C R B ) ) ) $=
    ( wor wcel wa wo wceq wn sonr adantrr pm1.2 nsyl breq2 breq1 orbi12d notbid
    wbr syl5ibcom con2d w3o solin 3orass or12 df-or 3bitri sylib impbid con2bid
    wi ) ADEZBAFZCAFZGGZBCDSZCBDSZHZBCIZUOURUSJZUOUSURUOBBDSZVAHZJUSURJUOVAVBUL
    UMVAJUNABDKLVAMNUSVBURUSVAUPVAUQBCBDOBCBDPQRTUAUOUPUSUQUBZUTURUKZABCDUCVCUP
    USUQHHUSURHVDUPUSUQUDUPUSUQUEUSURUFUGUHUIUJ $.
    $( [25-Jul-2011] $) $( [9-Apr-1996] $)

  $( Trichotomy law for strict order relation. $)
  sotrieq2 $p |- ( ( R Or A /\ ( B e. A /\ C e. A ) ) ->
              ( B = C <-> ( -. B R C /\ -. C R B ) ) ) $=
    ( wor wcel wa wceq wbr wo wn sotrieq ioran syl6bb ) ADEBAFCAFGGBCHBCDIZCBDI
    ZJKOKPKGABCDLOPMN $.
    $( [5-May-1999] $)

  ${
    $d x y R $.  $d x y A $.  $d x y ph $.
    issod.1 $e |- ( ph -> R Po A ) $.
    issod.2 $e |- ( ( ph /\ ( x e. A /\ y e. A ) ) ->
                    ( x R y \/ x = y \/ y R x ) ) $.
    $( A irreflexive, transitive, linear relation is a strict ordering. $)
    issod $p |- ( ph -> R Or A ) $=
      ( wpo cv wbr wceq w3o wral wor ralrimivva df-so sylanbrc ) ADEHBIZCIZEJRS
      KSREJLZCDMBDMDENFATBCDDGOBCDEPQ $.
      $( [9-Jul-2014] $) $( [21-Jan-1996] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    issoi.1 $e |- ( x e. A -> -. x R x ) $.
    issoi.2 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                  ( ( x R y /\ y R z ) -> x R z ) ) $.
    issoi.3 $e |- ( ( x e. A /\ y e. A ) -> ( x R y \/ x = y \/ y R x ) ) $.
    $( A irreflexive, transitive, linear relation is a strict ordering. $)
    issoi $p |- R Or A $=
      ( wor wtru cv wcel wbr wn adantl w3a wa wi ispod wceq w3o issod trud ) DE
      IJABDEJABCDEAKZDLZUDUDEMNJFOUEBKZDLZCKZDLPUDUFEMZUFUHEMQUDUHEMRJGOSUEUGQU
      IUDUFTUFUDEMUAJHOUBUC $.
      $( [9-Jul-2014] $) $( [21-Jan-1996] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.
    isso2i.1 $e |- ( ( x e. A /\ y e. A ) ->
                     ( x R y <-> -. ( x = y \/ y R x ) ) ) $.
    isso2i.2 $e |- ( ( x e. A /\ y e. A /\ z e. A ) ->
                     ( ( x R y /\ y R z ) -> x R z ) ) $.
    $( Deduce strict ordering from its properties. $)
    isso2i $p |- R Or A $=
      ( cv wcel wbr wn wa wceq wo eqid orci wb wi eleq1 anbi2d eqeq2 notbid w3o
      breq1 orbi12d bibi12d imbi12d con2bid chvarv anidms biimprd 3orass sylibr
      breq2 mpbii df-or bitri issoi ) ABCDEAHZDIZUSUSEJZKZUTUTLZUSUSMZVANZVBVDV
      AUSOPUTBHZDIZLZUSVFMZVFUSEJZNZUSVFEJZKZQZRVCVEVBQZRBAVFUSMZVHVCVNVOVPVGUT
      UTVFUSDSTVPVKVEVMVBVPVIVDVJVAVFUSUSUAVFUSUSEUDUEVPVLVAVFUSUSEUNUBUFUGVHVL
      VKFUHZUIUOUJGVHVMVKRZVLVIVJUCZVHVKVMVQUKVSVLVKNVRVLVIVJULVLVKUPUQUMUR $.
      $( [9-Jul-2014] $) $( [29-Jan-1996] $)
  $}

  ${
    $d x y R $.
    $( Any relation is a strict ordering of the empty set.  (The proof was
       shortened by Andrew Salmon, 25-Jul-2011.) $)
    so0 $p |- R Or (/) $=
      ( vx vy c0 wor wpo cv wbr wceq w3o wral df-so po0 ral0 mpbir2an ) DAEDAFB
      GZCGZAHPQIQPAHJCDKZBDKBCDALAMRBNO $.
      $( [25-Jul-2011] $) $( [16-Mar-1997] $)
  $}

