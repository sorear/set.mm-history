$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Proper substitution of classes for sets into classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c [_ $.  $( Underlined left bracket $)
  $c ]_ $.  $( Underlined right bracket $)

  $( Extend class notation to include the proper substitution of a class for a
     set into another class. $)
  csb $a class [_ A / x ]_ B $.

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( Define the proper substitution of a class for a set into another class.
       The underlined brackets distinguish it from the substitution into a wff,
       ~ wsbc , to prevent ambiguity.  Theorem ~ sbcel1g shows an example of
       how ambiguity could arise if we didn't use distinguished brackets.
       Theorem ~ sbccsbg recreates substitution into a wff from this
       definition. $)
    df-csb $a |- [_ A / x ]_ B = { y | [ A / x ] y e. B } $.
  $}

  ${
    $d x y A $.  $d y B $.  $d x y $.
    csb2.1 $e |- A e. _V $.
    $( Alternate expression for the proper substitution into a class, without
       referencing substitution into a wff.  Note that ` x ` can be free in
       ` B ` but cannot occur in ` A ` . $)
    csb2 $p |- [_ A / x ]_ B = { y | E. x ( x = A /\ y e. B ) } $=
      ( csb cv wcel wsbc cab wceq wa wex df-csb sbc5 abbii eqtri ) ACDFBGDHZACI
      ZBJAGCKRLAMZBJABCDNSTBRACEOPQ $.
      $( [2-Dec-2013] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.  $d y C $.
    $( Analog of ~ dfsbcq for proper substitution into a class. $)
    csbeq1 $p |- ( A = B -> [_ A / x ]_ C = [_ B / x ]_ C ) $=
      ( vy wceq cv wcel wsbc cab csb dfsbcq abbidv df-csb 3eqtr4g ) BCFZEGDHZAB
      IZEJQACIZEJABDKACDKPRSEQABCLMAEBDNAECDNO $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.  $d z V $.
    cbvcsbv.1 $e |- ( x = y -> B = C ) $.
    $( Change the bound variable of a proper substitution into a class using
       implicit substitution. $)
    cbvcsbv $p |- ( A e. V -> [_ A / x ]_ B = [_ A / y ]_ C ) $=
      ( vz wcel cv wsbc cab csb wceq eleq2d cbvsbcv abbidv df-csb 3eqtr4g ) CFI
      ZHJZDIZACKZHLUAEIZBCKZHLACDMBCEMTUCUEHUBUDABCFAJBJNDEUAGOPQAHCDRBHCERS $.
      $( [30-Sep-2008] $)
  $}

  ${
    csbeq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for proper substitution into a class. $)
    csbeq1d $p |- ( ph -> [_ A / x ]_ C = [_ B / x ]_ C ) $=
      ( wceq csb csbeq1 syl ) ACDGBCEHBDEHGFBCDEIJ $.
      $( [3-Dec-2005] $)
  $}

  ${
    $d x y $.  $d y A $.
    $( Analog of ~ sbid for proper substitution into a class. $)
    csbid $p |- [_ x / x ]_ A = A $=
      ( vy cv csb wcel wsbc cab df-csb sbid abbii abid2 3eqtri ) AADZBECDBFZANG
      ZCHOCHBACNBIPOCOAJKCBLM $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d x y $.  $d y A $.  $d y B $.
    $( Equality theorem for proper substitution into a class. $)
    csbeq1a $p |- ( x = A -> B = [_ A / x ]_ B ) $=
      ( cv wceq csb csbid csbeq1 syl5eqr ) ADZBECAJCFABCFACGAJBCHI $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d z A $.  $d y z B $.  $d z V $.  $d x z $.
    $( Composition law for chained substitutions into a class. $)
    csbcog $p |- ( A e. V -> [_ A / y ]_ [_ y / x ]_ B = [_ A / x ]_ B ) $=
      ( vz wcel csb wsbc cab df-csb abeq2i sbcbii sbccog bitrd abbidv 3eqtr4g
      cv ) CEGZFRZABRZDHZGZBCIZFJTDGZACIZFJBCUBHACDHSUDUFFSUDUEAUAIZBCIUFUCUGBC
      EUGFUBAFUADKLMUEABCENOPBFCUBKAFCDKQ $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d y A $.  $d y B $.  $d x y $.
    $( The existence of proper substitution into a class. $)
    csbexg $p |- ( ( A e. V /\ A. x B e. W ) -> [_ A / x ]_ B e. _V ) $=
      ( vy wcel wal wa csb wsbc cab cvv df-csb abid2 elex syl5eqel alimi a4sbc
      cv syl5 imp wb ax-17 sbcabel adantr mpbid ) BDGZCEGZAHZIZABCJFTZCGZABKFLZ
      MAFBCNUKUMFLZMGZABKZUNMGZUHUJUQUJUPAHUHUQUIUPAUIUOCMFCOCEPQRUPABDSUAUBUHU
      QURUCUJUMAFFBMDULMGAUDUEUFUGQ $.
      $( [10-Nov-2005] $)
  $}

  ${
    csbex.1 $e |- A e. _V $.
    csbex.2 $e |- B e. _V $.
    $( The existence of proper substitution into a class.  (The proof was
       shortened by Andrew Salmon, 29-Jun-2011.) $)
    csbex $p |- [_ A / x ]_ B e. _V $=
      ( cvv wcel csb wal csbexg mpan mpg ) CFGZABCHFGZABFGMAINDABCFFJKEL $.
      $( [29-Jun-2011] $) $( [7-Aug-2007] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y V $.  $d x y $.
    csbconstgf.1 $e |- ( y e. B -> A. x y e. B ) $.
    $( Substitution doesn't affect a constant ` B ` (in which ` x ` is not
       free). $)
    csbconstgf $p |- ( A e. V -> [_ A / x ]_ B = B ) $=
      ( wcel csb cv wsbc cab df-csb sbcgf abbi1dv syl5eq ) CEGZACDHBIDGZACJZBKD
      ABCDLPRBDQACEFMNO $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d A y $.  $d B x y $.  $d V y $.
    $( Substitution doesn't affect a constant ` B ` (in which ` x ` is not
       free). ~ csbconstgf with distinct variable requirement.(Contributed by
       Alan Sare, 22-Jul-2012.) $)
    csbconstgfv $p |- ( A e. V -> [_ A / x ]_ B = B ) $=
      ( vy cv wcel ax-17 csbconstgf ) AEBCDEFCGAHI $.
      $( [22-Jul-2012] $)
  $}

  ${
    $d w x y z $.  $d w y z A $.  $d w y z B $.  $d w y z C $.
    $( Distribute proper substitution through a membership relation.  (The
       proof was shortened by Andrew Salmon, 29-Jun-2011.) $)
    sbcel12g $p |- ( A e. V -> ( [ A / x ] B e. C <->
                   [_ A / x ]_ B e. [_ A / x ]_ C ) ) $=
      ( vy vz vw wcel wsbc cv cab csb dfsbcq wceq abbidv eleq12d hbs1 hbab sbab
      hbel sbie vtoclbg df-csb eleq12i syl6bbr ) BEICDIZABJZFKZCIZABJZFLZUIDIZA
      BJZFLZIZABCMZABDMZIUGAGKZJUJAUSJZFLZUMAUSJZFLZIZUHUPGBEUGAUSBNUSBOZVAULVC
      UOVEUTUKFUJAUSBNPVEVBUNFUMAUSBNPQUGVDAGAHHVAVCUTAFHUJAGRSVBAFHUMAGRSUAAKU
      SOCVADVCAGFCTAGFDTQUBUCUQULURUOAFBCUDAFBDUDUEUF $.
      $( [29-Jun-2011] $) $( [10-Nov-2005] $)

    $( Distribute proper substitution through an equality relation.  (The proof
       was shortened by Andrew Salmon, 29-Jun-2011.) $)
    sbceqg $p |- ( A e. V -> ( [ A / x ] B = C <->
                   [_ A / x ]_ B = [_ A / x ]_ C ) ) $=
      ( vz vy vw wcel wceq wsbc cv cab csb dfsbcq abbidv eqeq12d hbs1 hbab sbab
      hbeq sbie vtoclbg df-csb eqeq12i syl6bbr ) BEICDJZABKZFLZCIZABKZFMZUIDIZA
      BKZFMZJZABCNZABDNZJUGAGLZKUJAUSKZFMZUMAUSKZFMZJZUHUPGBEUGAUSBOUSBJZVAULVC
      UOVEUTUKFUJAUSBOPVEVBUNFUMAUSBOPQUGVDAGAHHVAVCUTAFHUJAGRSVBAFHUMAGRSUAALU
      SJCVADVCAGFCTAGFDTQUBUCUQULURUOAFBCUDAFBDUDUEUF $.
      $( [29-Jun-2011] $) $( [10-Nov-2005] $)
  $}

  $( Distribute proper substitution through negated membership.  (Contributed
     by Andrew Salmon, 18-Jun-2011.) $)
  sbcnel12g $p |- ( A e. V -> ( [ A / x ] B e/ C <-> [_ A / x ]_ B e/
                    [_ A / x ]_ C ) ) $=
    ( wcel wnel wsbc wn csb df-nel sbcbii sbcng sbcel12g notbid syl6bbr 3bitrd
    ) BEFZCDGZABHCDFZIZABHTABHZIZABCJZABDJZGZSUAABECDKLTABEMRUCUDUEFZIUFRUBUGAB
    CDENOUDUEKPQ $.
    $( [18-Jun-2011] $)

  $( Distribute proper substitution through an inequality.  (Contributed by
     Andrew Salmon, 18-Jun-2011.) $)
  sbcne12g $p |- ( A e. V -> ( [ A / x ] B =/= C <-> [_ A / x ]_ B =/=
                   [_ A / x ]_ C ) ) $=
    ( wcel wne wsbc csb wn wceq nne sbcbii sbcng sbceqg syl6bbr 3bitr3d con4bid
    ) BEFZCDGZABHZABCIZABDIZGZSTJZABHCDKZABHZUAJUDJZUEUFABECDLMTABENSUGUBUCKUHA
    BCDEOUBUCLPQR $.
    $( [18-Jun-2011] $)

  ${
    $d y A $.  $d x y C $.  $d y V $.
    $( Move proper substitution in and out of a membership relation.  Note that
       the scope of ` [ A / x ] ` is the wff ` B e. C ` , whereas the scope of
       ` [_ A / x ]_ ` is the class ` B ` . $)
    sbcel1g $p |- ( A e. V -> ( [ A / x ] B e. C <->
                    [_ A / x ]_ B e. C ) ) $=
      ( wcel wsbc csb sbcel12g csbconstgfv eleq2d bitrd ) BEFZCDFABGABCHZABDHZF
      NDFABCDEIMODNABDEJKL $.
      $( [10-Nov-2005] $)

    $( Move proper substitution to first argument of an equality. $)
    sbceq1g $p |- ( A e. V -> ( [ A / x ] B = C <->
                    [_ A / x ]_ B = C ) ) $=
      ( wcel wceq wsbc csb sbceqg csbconstgfv eqeq2d bitrd ) BEFZCDGABHABCIZABD
      IZGODGABCDEJNPDOABDEKLM $.
      $( [30-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y V $.
    $( Move proper substitution in and out of a membership relation. $)
    sbcel2g $p |- ( A e. V -> ( [ A / x ] B e. C <->
                    B e. [_ A / x ]_ C ) ) $=
      ( wcel wsbc csb sbcel12g csbconstgfv eleq1d bitrd ) BEFZCDFABGABCHZABDHZF
      COFABCDEIMNCOABCEJKL $.
      $( [14-Nov-2005] $)

    $( Move proper substitution to second argument of an equality. $)
    sbceq2g $p |- ( A e. V -> ( [ A / x ] B = C <->
                    B = [_ A / x ]_ C ) ) $=
      ( wcel wceq wsbc csb sbceqg csbconstgfv eqeq1d bitrd ) BEFZCDGABHABCIZABD
      IZGCPGABCDEJNOCPABCEKLM $.
      $( [30-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d x z B $.  $d z C $.  $d x y $.
    $( Commutative law for double substitution into a class. $)
    csbcomg $p |- ( ( A e. V /\ B e. W ) ->
                 [_ A / x ]_ [_ B / y ]_ C = [_ B / y ]_ [_ A / x ]_ C ) $=
      ( vz wcel cvv csb wceq elex wa cv wsbc wb sbcel2g sbcbidv 3bitr3d sbccomg
      ancoms adantr adantl eqrdv syl2an ) CFICJIZDJIZACBDEKZKZBDACEKZKZLDGICFMD
      GMUGUHNZHUJULUMHOZUIIZACPZUNUKIZBDPZUNUJIZUNULIZUMUNEIZBDPZACPZVAACPZBDPU
      PURVAABCDJJUAUHUGVCUPQUHVBUOACJBDUNEJRSUBUGVDUQBDJACUNEJRSTUGUPUSQUHACUNU
      IJRUCUHURUTQUGBDUNUKJRUDTUEUF $.
      $( [14-Nov-2005] $)
  $}

  ${
    csbeq2d.1 $e |- ( ph -> A. x ph ) $.
    csbeq2d.2 $e |- ( ph -> B = C ) $.
    $( Formula-building deduction rule for class substitution. $)
    csbeq2d $p |- ( ( ph /\ A e. V ) -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( wcel csb wceq wsbc wal alrimi a4sbc syl5 sbceqg sylibd impcom ) CFIZABC
      DJBCEJKZTADEKZBCLZUAAUBBMTUCAUBBGHNUBBCFOPBCDEFQRS $.
      $( [22-Nov-2005] $)
  $}

  ${
    $d x ph $.
    csbeq2dv.1 $e |- ( ph -> B = C ) $.
    $( Formula-building deduction rule for class substitution. $)
    csbeq2dv $p |- ( ( ph /\ A e. V ) -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( ax-17 csbeq2d ) ABCDEFABHGI $.
      $( [10-Nov-2005] $)
  $}

  ${
    csbeq2i.1 $e |- B = C $.
    $( Formula-building inference rule for class substitution. $)
    csbeq2i $p |- ( A e. V -> [_ A / x ]_ B = [_ A / x ]_ C ) $=
      ( wcel wceq wsbc csb sbcth sbceqg mpbid ) BEGCDHZABIABCJABDJHNABEFKABCDEL
      M $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d x y z $.
    $( The proper substitution of a class for set variable results in the class
       (if the class exists). $)
    csbvarg $p |- ( A e. V -> [_ A / x ]_ x = A ) $=
      ( vy vz wcel cvv cv csb wceq elex vex wsbc df-csb sbcel2gv abbi1dv syl5eq
      cab ax-mp csbeq2i csbcog 3eqtr3d syl ) BCFBGFZABAHZIZBJBCKUDDBADHZUEIZIDB
      UGIZUFBDBUHUGGUGGFZUHUGJDLUJUHEHZUEFAUGMZERUGAEUGUENUJULEUGAUKUGGOPQSTADB
      UEGUAUDUIUKUGFDBMZERBDEBUGNUDUMEBDUKBGOPQUBUC $.
      $( [10-Nov-2005] $)
  $}

  ${
    $d x y $.
    $( Substitution into a wff expressed in terms of substitution into a
       class. $)
    sbccsbg $p |- ( A e. V ->
                 ( [ A / x ] ph <-> y e. [_ A / x ]_ { y | ph } ) ) $=
      ( wcel cv cab wsbc csb abid sbcbii sbcel2g bitr3d ) DEFCGZACHZFZBDIABDIOB
      DPJFQABDEACKLBDOPEMN $.
      $( [15-Aug-2007] $)
  $}

  $( Substitution into a wff expressed in using substitution into a class. $)
  sbccsb2g $p |- ( A e. V ->
               ( [ A / x ] ph <-> A e. [_ A / x ]_ { x | ph } ) ) $=
    ( wcel cv cab wsbc csb sbcel12g abid sbcbii csbvarg eleq1d 3bitr3d ) CDEZBF
    ZABGZEZBCHBCQIZBCRIZEABCHCUAEBCQRDJSABCDABKLPTCUABCDMNO $.
    $( [27-Nov-2005] $)

  ${
    $d y z A $.  $d z B $.  $d x y z $.
    hbcsb1g.1 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsb1g $p |- ( A e. V ->
                 ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) ) $=
      ( vz wcel cvv cv csb wal wi elex wsbc cab ax-17 hbel alrimi hbsbc1g hbabd
      df-csb eleq2i albii 3imtr4g syl ) CEHCIHZBJZACDKZHZUJALZMCENUGUHGJDHZACOZ
      GPZHZUOALUJUKUGUMAGBUGUGGLAABBCIFUHIHAQRUGGQSULABCIFTUAUIUNUHAGCDUBUCZUJU
      OAUPUDUEUF $.
      $( [11-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d z B $.  $d x y z $.
    hbcsb1.1 $e |- A e. _V $.
    hbcsb1.2 $e |- ( y e. A -> A. x y e. A ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsb1 $p |- ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) $=
      ( cvv wcel cv csb wal wi hbcsb1g ax-mp ) CGHBIACDJHZOAKLEABCDGFMN $.
      $( [17-Aug-2006] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x z $.  $d w y $.
    hbcsb.1 $e |- ( z e. A -> A. x z e. A ) $.
    hbcsb.2 $e |- ( z e. B -> A. x z e. B ) $.
    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsb $p |- ( z e. [_ A / y ]_ B -> A. x z e. [_ A / y ]_ B ) $=
      ( vw csb cv wcel wsbc cab df-csb hblem hbsbc hbab hbxfreq ) ACBDEIHJEKZBD
      LZHMBHDENTAHCSABCDFACHEGOPQR $.
      $( [7-Sep-2014] $)

    $( Bound-variable hypothesis builder for substitution into a class. $)
    hbcsbg $p |- ( A e. V ->
                 ( z e. [_ A / y ]_ B -> A. x z e. [_ A / y ]_ B ) ) $=
      ( vw wcel cvv cv csb wal wi elex wsbc cab ax-17 hbel alrimi hbsbcg df-csb
      hblem hbabd eleq2i albii 3imtr4g syl ) DFJDKJZCLZBDEMZJZUMANZODFPUJUKILEJ
      ZBDQZIRZJZURANUMUNUJUPAICUJUJINAACCDKGUKKJASTUJISUAUOABCDKGACIEHUDUBUEULU
      QUKBIDEUCUFZUMURAUSUGUHUI $.
      $( [20-Nov-2005] $)
  $}

  ${
    $d y z A $.  $d z B $.  $d y z ph $.  $d x y z $.
    hbcsb1gd.1 $e |- ( ph -> A. x ph ) $.
    hbcsb1gd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    $( Deduction version of ~ hbcsb1g . $)
    hbcsb1gd $p |- ( ( ph /\ A e. V ) ->
                 ( y e. [_ A / x ]_ B -> A. x y e. [_ A / x ]_ B ) ) $=
      ( vz wcel cvv cv csb wal wi elex wa a1d a17d hbeld hband anabsi5 hbsbc1gd
      wsbc wb sbcel2g adantl albid 3imtr3d sylan2 ) DFJADKJZCLZBDEMZJZUNBNODFPA
      UKQZBIULUMAUKUOBNAAUKBAABNAGRABCDKGHAULKJBSTUAUBZUOILZULJBSUOUQEJZBDUDZUS
      BNUQUMJZUTBNAURBCDKGHUCUKUSUTUEABDUQEKUFUGZUOUSUTBUPVAUHUITUJ $.
      $( [23-May-2006] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w z ph $.  $d w x z $.  $d w y $.
    hbcsbgd.1 $e |- ( ph -> A. x ph ) $.
    hbcsbgd.2 $e |- ( ph -> A. y ph ) $.
    hbcsbgd.3 $e |- ( ph -> ( z e. A -> A. x z e. A ) ) $.
    hbcsbgd.4 $e |- ( ph -> ( z e. B -> A. x z e. B ) ) $.
    $( Deduction version of ~ hbcsbg . $)
    hbcsbgd $p |- ( ( ph /\ A e. V ) ->
                 ( z e. [_ A / y ]_ B -> A. x z e. [_ A / y ]_ B ) ) $=
      ( vw wcel cvv cv wal wi ax-17 a1i hbeld csb elex wa hband anabsi5 hbsbcgd
      a1d wsbc wb sbcel2g adantl albid 3imtr3d sylan2 ) EGMAENMZDOZCEFUAZMZURBP
      QEGUBAUOUCZBLUPUQAUOUSBPAAUOBAABPAHUGABDENHJUPNMZUTBPQAUTBRSTUDUEZLOZUPMZ
      VCBPQUSVCBRSUSVBFMZCEUHZVEBPVBUQMZVFBPAVDBCDENHIJABDVBFHUPVBMZVGBPQAVGBRS
      KTUFUOVEVFUIACEVBFNUJUKZUSVEVFBVAVHULUMTUN $.
      $( [21-Nov-2005] $)
  $}

  ${
    $d z A $.  $d w B $.  $d z C $.  $d w x y z $.
    csbhypf.1 $e |- ( z e. A -> A. x z e. A ) $.
    csbhypf.2 $e |- ( z e. C -> A. x z e. C ) $.
    csbhypf.3 $e |- ( x = A -> B = C ) $.
    $( Introduce an explicit substitution into an implicit substitution
       hypothesis.  See ~ sbhypf for class substitution version. $)
    csbhypf $p |- ( y = A -> [_ y / x ]_ B = C ) $=
      ( vw cv wceq wa wex csb wcel ax-17 vex eqvincf hbcsb1 hbeq csbeq1a exlimi
      sylan9req sylbi ) BKZDLAKZUFLZUGDLZMZANAUFEOZFLZACUFDCKUFPAQGBRZSUJULAAJC
      UKFAJUFEUMJKUFPAQTHUAUHUIUKEFAUFEUBIUDUCUE $.
      $( [19-Dec-2008] $)
  $}

  ${
    $d x z A $.  $d z B $.  $d y z C $.  $d z V $.  $d x y z $.
    $( Conversion of implicit substitution to explicit substitution into a
       class.  (Closed theorem version of ~ csbiegf .) $)
    csbiegft $p |- ( ( A e. V /\ A. x A. y ( y e. C -> A. x y e. C ) /\
                 A. x ( x = A -> B = C ) ) -> [_ A / x ]_ B = C ) $=
      ( vz wcel cv wal wi wceq w3a csb wsbc cab df-csb wb id alimi eleq1 albidv
      imbi12d a4v eleq2 imim2i sbciegft syl3an abbi1dv syl5eq ) CFHZBIZEHZUMAJZ
      KZBJZAJZAICLZDELZKZAJZMZACDNGIZDHZACOZGPEAGCDQVBVEGEUKUKUQVCEHZVFAJZKZAJV
      AURVDVFRZKZAJVEVFRUKSUPVHAUOVHBGULVCLZUMVFUNVGULVCEUAZVKUMVFAVLUBUCUDTUTV
      JAUSVIURDEVCUEUFTVDVFACFUGUHUIUJ $.
      $( [11-Nov-2005] $)
  $}

  ${
    $d x z A $.  $d z B $.  $d y C $.  $d x y $.
    csbieb.1 $e |- A e. _V $.
    csbieb.2 $e |- ( y e. C -> A. x y e. C ) $.
    $( Bidirectional conversion between an implicit class substitution
       hypothesis ` x = A -> B = C ` and its explicit substitution
       equivalent. $)
    csbieb $p |- ( A. x ( x = A -> B = C ) <-> [_ A / x ]_ B = C ) $=
      ( vz wceq csb cv wcel ax-17 hbcsb1 hbeq csbeq1a eqeq1d ceqsal ) DEIACDJZE
      IACAHBSEAHCDFHKCLAMNGOFAKCIDSEACDPQR $.
      $( [2-Mar-2008] $)
  $}

  ${
    $d a x A $.  $d a B $.  $d a y C $.  $d x y $.
    csbiebg.2 $e |- ( y e. C -> A. x y e. C ) $.
    $( Bidirectional conversion between an implicit class substitution
       hypothesis ` x = A -> B = C ` and its explicit substitution
       equivalent. $)
    csbiebg $p |- ( A e. V ->
          ( A. x ( x = A -> B = C ) <-> [_ A / x ]_ B = C ) ) $=
      ( va cv wceq wi wal csb eqeq2 imbi1d albidv csbeq1 eqeq1d vex csbieb
      vtoclbg ) AIZHIZJZDEJZKZALAUCDMZEJUBCJZUEKZALACDMZEJHCFUCCJZUFUIAUKUDUHUE
      UCCUBNOPUKUGUJEAUCCDQRABUCDEHSGTUA $.
      $( [24-Mar-2013] $)
  $}

  ${
    $d x y A $.  $d y C $.  $d x y V $.
    csbiegf.1 $e |- ( A e. V -> ( y e. C -> A. x y e. C ) ) $.
    csbiegf.2 $e |- ( x = A -> B = C ) $.
    $( Conversion of implicit substitution to explicit substitution into a
       class. $)
    csbiegf $p |- ( A e. V -> [_ A / x ]_ B = C ) $=
      ( wcel cv wal wi wceq wa csb alrimivv ax-gen jctir csbiegft 3expb mpdan )
      CFIZBJEIZUCAKLZBKAKZAJCMDEMLZAKZNACDOEMZUBUEUGUBUDABGPUFAHQRUBUEUGUHABCDE
      FSTUA $.
      $( [11-Nov-2005] $)
  $}

  ${
    $d x A $.  $d y C $.  $d x y $.
    csbief.1 $e |- A e. _V $.
    csbief.2 $e |- ( y e. C -> A. x y e. C ) $.
    csbief.3 $e |- ( x = A -> B = C ) $.
    $( Conversion of implicit substitution to explicit substitution into a
       class.  (The proof was shortened by Andrew Salmon, 9-Jul-2011.) $)
    csbief $p |- [_ A / x ]_ B = C $=
      ( cv wceq wi csb csbieb mpgbi ) AICJDEJKACDLEJAABCDEFGMHN $.
      $( [9-Jul-2011] $) $( [26-Nov-2005] $)
  $}

  ${
    $d x A $.  $d x y C $.  $d x y ph $.
    csbied.1 $e |- ( ph -> A e. V ) $.
    csbied.2 $e |- ( ( ph /\ x = A ) -> B = C ) $.
    $( Conversion of implicit substitution to explicit substitution into a
       class.  (Contributed by Mario Carneiro, 2-Dec-2014.) $)
    csbied $p |- ( ph -> [_ A / x ]_ B = C ) $=
      ( vy wcel cv wal wi wceq csb a17d alrimivv ex alrimiv csbiegft syl3anc )
      ACFJIKEJZUBBLMZILBLBKCNZDENZMZBLBCDOENGAUCBIAUBBPQAUFBAUDUEHRSBICDEFTUA
      $.
      $( [2-Dec-2014] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.  $d x y z D $.
    csbie2g.1 $e |- A e. _V $.
    csbie2g.2 $e |- B e. _V $.
    $( Conversion of implicit substitution to explicit substitution into a
       class (closed form of ~ csbie2 ). $)
    csbie2t $p |- ( A. x A. y ( ( x = A /\ y = B ) -> C = D ) ->
                  [_ A / x ]_ [_ B / y ]_ C = D ) $=
      ( vz cv wceq wa wi wal wex csb isseti hba1 wcel ax-17 eeanv hbcsb1 hbcsbg
      hbeq cvv ax-mp csbeq1a sylan9eqr pm3.43 mpan eqtr2 syl6 a4s exlimd mp2ani
      syl5bir ) AJCKZBJDKZLZEFKZMZBNZANZUQAOZURBOZACBDEPZPZFKZACGQBDHQVDVELUSBO
      ZAOVCVHUQURABUAVCVIVHAVBARAIIVGFAICVFGIJZCSZATUBVJFSZATUDVBVIVHMAVBUSVHBV
      ABRBIIVGFCUESVJVGSZVMBNMGBAICVFUEVKBTBIDEHVJDSBTUBUCUFVLBTUDVAUSVHMBVAUSE
      VGKZUTLZVHUSVNMVAUSVOMURUQEVFVGBDEUGACVFUGUHUSVNUTUIUJEVGFUKULUMUNUMUNUPU
      O $.
      $( [3-Sep-2007] $)

    ${
      csbie2.3 $e |- ( ( x = A /\ y = B ) -> C = D ) $.
      $( Conversion of implicit substitution to explicit substitution into a
         class. $)
      csbie2 $p |- [_ A / x ]_ [_ B / y ]_ C = D $=
        ( cv wceq wa wi wal csb gen2 csbie2t ax-mp ) AJCKBJDKLEFKMZBNANACBDEOOF
        KSABIPABCDEFGHQR $.
        $( [27-Aug-2007] $)
    $}
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.  $d x y z V $.  $d y z W $.
    $( Lemma for ~ csbnestg . $)
    csbnestglem $p |- ( ( A e. V /\ A. x B e. W ) ->
                  [_ A / x ]_ [_ B / y ]_ C = [_ [_ A / x ]_ B / y ]_ C ) $=
      ( vz wcel wal wa cv csb wi wceq simpl ax-17 hba1 hban cvv hbcsb1g hbcsbgd
      csbexg adantr a17d alrimiv alrimi csbeq1a csbeq1d ax-gen csbiegft syl3anc
      mpdan a1i ) CFIZDGIZAJZKZUOHLZBACDMZEMZIZVBAJNZHJZAJALCOZBDEMZVAONZAJZACV
      FMVAOUOUQPURVDAUOUQAUOAQUPARSZURVCHURUTTIVCACDFGUCURABHUTETVIUOUQBUOBQUQB
      QSUOUSUTIZVJAJNUQAHCDFUSCIAQUAUDURUSEIAUEUBUMUFUGVHURVGAVEBDUTEACDUHUIUJU
      NAHCVFVAFUKUL $.
      $( [23-Nov-2005] $)
  $}

  ${
    $d w z A $.  $d w z B $.  $d w x z C $.  $d w x y $.
    $( Nest the composition of two substitutions. $)
    csbnestg $p |- ( ( A e. V /\ A. x B e. W ) ->
                  [_ A / x ]_ [_ B / y ]_ C = [_ [_ A / x ]_ B / y ]_ C ) $=
      ( vz vw wcel cvv wal csb wceq elex cv csbcog adantr csbnestglem mpan hba1
      alimi vex csbeq2dv ancoms csbexg alrimiv sylan2 csbeq1d 3eqtrd eqtr3d a4s
      wa csbeq2d syl 3eqtr3d syl2an ) CFJCKJZDKJZALZACBDEMZMZBACDMZEMZNDGJZALCF
      OVEUSADGOUBURUTUMZACHDBHPEMZMZMZHVCVGMZVBVDVFICAIPZVHMZMZVIVJURVMVINUTAIC
      VHKQRVFVMICHAVKDMZVGMZMZHICVNMZVGMZVJUTURVMVPNUTICVLVOKVKKJZUTVLVONIUCZAH
      VKDVGKKSTUDUEUTURVNKJZILVPVRNUTWAIVSUTWAVTAVKDKKUFTUGIHCVNVGKKSUHURVRVJNU
      TURHVQVCVGAICDKQUIRUJUKUTURVIVBNUTACVHVAKUSAUAUSVHVANABHDEKQULUNUEVFVCKJV
      JVDNACDKKUFBHVCEKQUOUPUQ $.
      $( [23-Nov-2005] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d y C $.  $d y W $.
    $( Nest the composition of two substitutions. $)
    csbnest1g $p |- ( ( A e. V /\ A. x B e. W ) ->
                  [_ A / x ]_ [_ B / x ]_ C = [_ [_ A / x ]_ B / x ]_ C ) $=
      ( vy wcel cvv wal csb wceq elex wa cv wi simpl ax-17 hba1 hban csbexg a1i
      hbcsb1g hbcsb1gd syldan alrimiv alrimi csbeq1a csbeq1d csbiegft syl3anc
      ax-gen sylan ) BEHBIHZCFHZAJZABACDKZKAABCKZDKZLZBEMUNUPNZUNGOZUSHZVCAJPZG
      JZAJAOBLZUQUSLPZAJZUTUNUPQVAVEAUNUPAUNARZUOASTVAVDGUNUPURIHVDABCIFUAUNAGU
      RDIVIAGBCIVBBHARUCUDUEUFUGVHVAVGAVFACURDABCUHUIULUBAGBUQUSIUJUKUM $.
      $( [23-May-2006] $)
  $}

  ${
    $d x ph $.  $d x y $.
    $( Nest the composition of two substitutions. $)
    sbcnestg $p |- ( ( A e. V /\ A. x B e. W ) ->
                ( [ A / x ] [ B / y ] ph <-> [ [_ A / x ]_ B / y ] ph ) ) $=
      ( wcel wal cvv wsbc csb wb elex alimi wa cab hba1 sbccsb2g a4s sbcbid syl
      ancoms sbcel12g adantr csbnestg eleq2d csbexg bitr4d 3bitrd sylan2 ) EGHZ
      BIDFHZEJHZBIZACEKZBDKZACBDELZKZMULUNBEGNOUMUOPZUQECEACQZLZHZBDKZURBDVBLZH
      ZUSUOUMUQVDMUOUPVCBDFUNBRUNUPVCMBACEJSTUAUCUMVDVFMUOBDEVBFUDUEUTVFURCURVA
      LZHZUSUTVEVGURBCDEVAFJUFUGUTURJHUSVHMBDEFJUHACURJSUBUIUJUK $.
      $( [27-Nov-2005] $)
  $}

  ${
    $d x y A $.
    $( Idempotent law for class substitutions. $)
    csbidmg $p |- ( A e. V -> [_ A / x ]_ [_ A / x ]_ B = [_ A / x ]_ B ) $=
      ( vy wcel cvv csb wceq elex wal ax-17 csbnest1g mpdan cv csbconstgf eqtrd
      csbeq1d syl ) BDFBGFZABABCHZHZUAIBDJTUBAABBHZCHZUATTAKUBUDITALABBCGGMNTAU
      CBCAEBBGEOBFALPRQS $.
      $( [1-Mar-2008] $)
  $}

  ${
    $d x A $.  $d x C $.  $d x z D $.  $d x y $.
    csbco3g.1 $e |- ( x = A -> B = D ) $.
    $( Composition of two class substitutions. $)
    csbco3g $p |- ( ( A e. V /\ A. x B e. W ) ->
                 [_ A / x ]_ [_ B / y ]_ C = [_ D / y ]_ C ) $=
      ( vz wcel wal wa csb csbnestg wceq cv wi ax-17 gen2 ax-gen mp3an23 adantr
      csbiegft csbeq1d eqtrd ) CGKZDHKALZMACBDENNBACDNZENZBFENZABCDEGHOUGUJUKPU
      HUGBUIFEUGJQFKZULALRZJLALAQCPDFPRZALUIFPUMAJULASTUNAIUAAJCDFGUDUBUEUCUF
      $.
      $( [27-Nov-2005] $)
  $}

  ${
    $d x A $.  $d x ph $.  $d x z C $.  $d x y $.
    sbcco3g.1 $e |- ( x = A -> B = C ) $.
    $( Composition of two substitutions. $)
    sbcco3g $p |- ( ( A e. V /\ A. x B e. W ) ->
                ( [ A / x ] [ B / y ] ph <-> [ C / y ] ph ) ) $=
      ( vz wcel wal wa wsbc csb sbcnestg wb wceq cv wi ax-17 gen2 ax-gen dfsbcq
      csbiegft mp3an23 syl adantr bitrd ) DGKZEHKBLZMACENBDNACBDEOZNZACFNZABCDE
      GHPUJUMUNQZUKUJULFRZUOUJJSFKZUQBLTZJLBLBSDREFRTZBLUPURBJUQBUAUBUSBIUCBJDE
      FGUEUFACULFUDUGUHUI $.
      $( [27-Nov-2005] $)
  $}

  ${
    $d x B $.  $d x D $.
    $( Special case related to ~ ra4sbc .  (The proof was shortened by Eric
       Schmidt, 17-Jan-2007.) $)
    ra4csbela $p |- ( ( A e. B /\ A. x e. B C e. D ) -> [_ A / x ]_ C e. D ) $=
      ( wcel wral csb wsbc ra4sbc sbcel1g sylibd imp ) BCFZDEFZACGZABDHEFZNPOAB
      IQOABCJABDECKLM $.
      $( [18-Jan-2007] $) $( [10-Dec-2005] $)
  $}

  ${
    $d y z A $.  $d z ph $.  $d x y z $.  $d V z $.
    $( Move substitution into a class abstraction.  (The proof was shortened by
       Andrew Salmon, 9-Jul-2011.) $)
    csbabg $p |- ( A e. V ->
                 [_ A / x ]_ { y | ph } = { y | [ A / x ] ph } ) $=
      ( vz wcel cab csb wsbc df-clab cvv vex sbccomg mpan syl5bb sbcbii sbcel2g
      cv wb 3bitr2rd eqrdv ) DEGZFBDACHZIZABDJZCHZUCFSZUGGZACUHJZBDJZUHUDGZBDJU
      HUEGUIUFCUHJZUCUKUFFCKUHLGUCUMUKTFMACBUHDLENOPULUJBDEAFCKQBDUHUDERUAUB $.
      $( [9-Jul-2011] $) $( [13-Dec-2005] $)
  $}

  ${
    $d x v z $.  $d y v z $.  $d A w v z $.  $d B w v z $.  $d ph v z $.
    $d ps v z $.
    cbvralcsf.1 $e |- ( x = y -> A = B ) $.
    cbvralcsf.2 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbvralcsf.3 $e |- ( w e. A -> A. y w e. A ) $.
    cbvralcsf.4 $e |- ( w e. B -> A. x w e. B ) $.
    cbvralcsf.5 $e |- ( ph -> A. y ph ) $.
    cbvralcsf.6 $e |- ( ps -> A. x ps ) $.
    $( A more general version of ~ cbvralf that doesn't require ` A ` and ` B `
       to be distinct from ` x ` or ` y ` .  Changes bound variables using
       implicit substitution.  (Contributed by Andrew Salmon, 13-Jul-2011.) $)
    cbvralcsf $p |- ( A. x e. A ph <-> A. y e. B ps ) $=
      ( vz vv cv wcel wi wal ax-17 wral csb wsbc hbcsb1 hblem hbs1 hbim wceq id
      vex csbeq1a eleq12d sbequ12 imbi12d cbval hbcsbg ax-mp hbsb csbeq1 df-csb
      cvv cab eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ syl6bb bitri df-ral
      3bitr4i ) CPZFQZARZCSZDPZGQZBRZDSZACFUABDGUAVQNPZCWBFUBZQZACWBUCZRZNSWAVP
      WFCNVPNTWDWECCONWCCOWBFNUJZOPZWBQZCTUDUEACNUFUGVNWBUHZVOWDAWEWJVNWBFWCWJU
      ICWBFUKULACNUMUNUOWFVTNDWDWEDDONWCWBVAQWHWCQZWKDSRWGDCOWBFVAWIDTDEOFJUEUP
      UQUEACNDLURUGVTNTWBVRUHZWDVSWEBWLWBVRWCGWLUIWLWCCVRFUBZGCWBVRFUSWMWHFQZCV
      RUCZOVBGCOVRFUTWOOGWOWHGQZWNWPCDCEOGKUEVNVRUHFGWHHVCVDVEVFVGVHULWLWEACVRU
      CBANDCVIABCDMIVDVJUNUOVKACFVLBDGVLVM $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrexf that has no distinct variable
       restrictions.  Changes bound variables using implicit substitution.
       (Contributed by Andrew Salmon, 13-Jul-2011.) $)
    cbvrexcsf $p |- ( E. x e. A ph <-> E. y e. B ps ) $=
      ( vz vv cv wcel wa ax-17 hblem wex wrex csb wsbc hbcsb1 hbs1 hban wceq id
      vex csbeq1a eleq12d sbequ12 anbi12d cbvex cvv wi hbcsbg ax-mp hbsb csbeq1
      wal cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ syl6bb bitri
      df-rex 3bitr4i ) CPZFQZARZCUAZDPZGQZBRZDUAZACFUBBDGUBVSNPZCWDFUCZQZACWDUD
      ZRZNUAWCVRWHCNVRNSWFWGCCONWECOWDFNUJZOPZWDQZCSUETACNUFUGVPWDUHZVQWFAWGWLV
      PWDFWEWLUICWDFUKULACNUMUNUOWHWBNDWFWGDDONWEWDUPQWJWEQZWMDVBUQWIDCOWDFUPWK
      DSDEOFJTURUSTACNDLUTUGWBNSWDVTUHZWFWAWGBWNWDVTWEGWNUIWNWECVTFUCZGCWDVTFVA
      WOWJFQZCVTUDZOVCGCOVTFVDWQOGWQWJGQZWPWRCDCEOGKTVPVTUHFGWJHVEVFVGVHVIVJULW
      NWGACVTUDBANDCVKABCDMIVFVLUNUOVMACFVNBDGVNVO $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvreuv that has no distinct variable
       rextrictions.  Changes bound variables using implicit substitution.
       (Contributed by Andrew Salmon, 13-Jul-2011.) $)
    cbvreucsf $p |- ( E! x e. A ph <-> E! y e. B ps ) $=
      ( vz vv cv wcel wa ax-17 hblem weu wreu csb wsbc hbcsb1 hbs1 hban wceq id
      vex csbeq1a eleq12d sbequ12 anbi12d cbveu cvv wi hbcsbg ax-mp hbsb csbeq1
      wal cab df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ syl6bb bitri
      df-reu 3bitr4i ) CPZFQZARZCUAZDPZGQZBRZDUAZACFUBBDGUBVSNPZCWDFUCZQZACWDUD
      ZRZNUAWCVRWHCNVRNSWFWGCCONWECOWDFNUJZOPZWDQZCSUETACNUFUGVPWDUHZVQWFAWGWLV
      PWDFWEWLUICWDFUKULACNUMUNUOWHWBNDWFWGDDONWEWDUPQWJWEQZWMDVBUQWIDCOWDFUPWK
      DSDEOFJTURUSTACNDLUTUGWBNSWDVTUHZWFWAWGBWNWDVTWEGWNUIWNWECVTFUCZGCWDVTFVA
      WOWJFQZCVTUDZOVCGCOVTFVDWQOGWQWJGQZWPWRCDCEOGKTVPVTUHFGWJHVEVFVGVHVIVJULW
      NWGACVTUDBANDCVKABCDMIVFVLUNUOVMACFVNBDGVNVO $.
      $( [13-Jul-2011] $)

    $( A more general version of ~ cbvrab with no distinct variable
       restrictions.  (Contributed by Andrew Salmon, 13-Jul-2011.) $)
    cbvrabcsf $p |- { x e. A | ph } = { y e. B | ps } $=
      ( vz vv cv wcel cab ax-17 hblem wa crab csb wsbc hbcsb1 hbs1 hban wceq id
      vex csbeq1a eleq12d sbequ12 anbi12d cbvab cvv wi hbcsbg ax-mp hbsb csbeq1
      df-csb eleq2d sbie bicomi abbi2i eqtr4i syl6eq sbequ syl6bb eqtri 3eqtr4i
      wal df-rab ) CPZFQZAUAZCRZDPZGQZBUAZDRZACFUBBDGUBVRNPZCWCFUCZQZACWCUDZUAZ
      NRWBVQWGCNVQNSWEWFCCONWDCOWCFNUJZOPZWCQZCSUETACNUFUGVOWCUHZVPWEAWFWKVOWCF
      WDWKUICWCFUKULACNUMUNUOWGWANDWEWFDDONWDWCUPQWIWDQZWLDVMUQWHDCOWCFUPWJDSDE
      OFJTURUSTACNDLUTUGWANSWCVSUHZWEVTWFBWMWCVSWDGWMUIWMWDCVSFUCZGCWCVSFVAWNWI
      FQZCVSUDZORGCOVSFVBWPOGWPWIGQZWOWQCDCEOGKTVOVSUHFGWIHVCVDVEVFVGVHULWMWFAC
      VSUDBANDCVIABCDMIVDVJUNUOVKACFVNBDGVNVL $.
      $( [13-Jul-2011] $)
  $}


