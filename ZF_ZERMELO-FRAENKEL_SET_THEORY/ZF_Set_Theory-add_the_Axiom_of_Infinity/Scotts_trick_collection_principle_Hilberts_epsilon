$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Scott's trick; collection principle; Hilbert's epsilon
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, is a set. $)
    scottex $p |- { x e. A | A. y e. A ( rank ` x ) C_ ( rank ` y ) } e. _V $=
      ( vz vw c0 wceq crnk cfv wss wral crab cvv wcel 0ex eleq1 syl ax-17 con0
      cv mpbiri rabexg wn wex neq0 hbra1 hbrab wi ra4 com12 a1d ralrimiv ss2rab
      hbel sylibr rankon fveq2 sseq1d elrab simprbi sseq2 ralbidv rcla4ev mp2an
      wrex rgen bndrank ax-mp ssex exlimi sylbi pm2.61i ) CFGZATZHIZBTZHIZJZBCK
      ZACLZMNZVMCMNZWAVMWBFMNOCFMPUAVSACMUBQVMUCVPCNZBUDWABCUEWCWABBDDVTMVSBADC
      VRBCUFDTZCNBRUGWDMNBRUNWCVTVRACLZJZWAWCVSVRUHZACKWFWCWGACWCWGVNCNVSWCVRVR
      BCUIUJUKULVSVRACUMUOVTWEETZHIZWDJZEWEKZDSVEZWEMNVQSNWIVQJZEWEKZWLVPUPWMEW
      EWHWENWHCNWMVRWMAWHCVNWHGVOWIVQVNWHHUQURUSUTVFWKWNDVQSWDVQGWJWMEWEWDVQWIV
      AVBVCVDDEWEVGVHVIQVJVKVL $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z w A $.
    $( Scott's trick collects all sets that have a certain property and are of
       smallest possible rank.  This theorem shows that the resulting
       collection, expressed as in Equation 9.3 of [Jech] p. 72, contains at
       least one representative with the property, if there is one.  In other
       words, the collection is empty iff no set has the property (i.e. ` A `
       is empty). $)
    scott0 $p |- ( A = (/) <->
               { x e. A | A. y e. A ( rank ` x ) C_ ( rank ` y ) } = (/) ) $=
      ( vz c0 wceq cv crnk cfv wss crab wne wrex wcel wex mpan2 sylibr syl con0
      sylbi wral rabeq rab0 syl6eq ciin n0 hbre1 eqid ra4e exlimi wa fvex eqeq1
      cab anbi2d cla4ev eximi excom df-rex exbii 3imtr4i abn0 cint dfiin2 hbab1
      wi ax-17 dfss2f rankon eleq1 mpbiri rexlimivw mpgbir onint syl5eqel hbii1
      abid mpan hbeleq rexbid elabg ibi fveq2 sseq1d rcla4ev iinss sseq1 syl5ib
      ssid ralrimiv reximi 3syl rabn0 necon4i impbii ) CEFZAGZHIZBGZHIZJZBCUAZA
      CKZEFWPXCXBAEKEXBACEUBXBAUCUDCEXCECELZXBACMZXCELXDACWRUEZWSWRFZACMZBUNZNZ
      XFWRFZACMZXEXDXIELZXJXDXHBOZXMXDWRWRFZACMZXNXDWQCNZAOXPACUFXQXPAXOACUGXQX
      OXPWRUHXOACUIPUJTXQXOUKZAOZXQXGUKZAOZBOZXPXNXSXTBOZAOYBXRYCAXTXRBWRWQHULZ
      XGXGXOXQWSWRWRUMUOUPUQXTBAURQXOACUSXHYABXGACUSUTVARXHBVBQXMXFXIVCZXIABCWR
      YDVDXISJZXMYEXINYFWSXINZWSSNZVFBBDXISXHBDVEDGSNBVGVHYGXHYHXHBVQXGYHACXGYH
      WRSNWQVIWSWRSVJVKVLTVMXIVNVRVORXJXLXHXLBXFXIWSXFFXGXKACABXFABCWRVPVSWSXFW
      RUMVTWAWBXKXBACXKXABCWSCNZXFWTJZXKXAYIXAACMZYJYIWTWTJZYKWTWIXAYLAWSCWQWSF
      WRWTWTWQWSHWCWDWEPACWRWTWFRXFWRWTWGWHWJWKWLXBACWMQWNWO $.
      $( [15-Oct-2003] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( Theorem scheme version of ~ scottex .  The collection of all ` x ` of
       minimum rank such that ` ph ( x ) ` is true, is a set. $)
    scottexs $p |- { x | ( ph /\
             A. y ( [ y / x ] ph -> ( rank ` x ) C_ ( rank ` y ) ) ) } e. _V $=
      ( vz cv crnk cfv wss cab wral crab wsbc wi wal cvv wcel ax-17 hbab1 hbral
      wa wceq fveq2 sseq1d ralbidv cbvrab df-rab abid df-ral imbi1i albii bitri
      df-clab anbi12i abbii 3eqtri scottex eqeltrri ) DEZFGZCEZFGZHZCABIZJZDVCK
      ZAABUTLZBEZFGZVAHZMZCNZTZBIZOVEVICVCJZBVCKVGVCPZVNTZBIVMVDVNDBCVCUTVCPZDQ
      ABCRZVBBCVCVRVBBQSVNDQURVGUAZVBVICVCVSUSVHVAURVGFUBUCUDUEVNBVCUFVPVLBVOAV
      NVKABUGVNVQVIMZCNVKVICVCUHVTVJCVQVFVIACBULUIUJUKUMUNUODCVCUPUQ $.
      $( [13-Oct-2003] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( Theorem scheme version of ~ scott0 .  The collection of all ` x ` of
       minimum rank such that ` ph ( x ) ` is true, is not empty iff there is
       an ` x ` such that ` ph ( x ) ` holds. $)
    scott0s $p |- ( E. x ph <-> { x | ( ph /\
         A. y ( [ y / x ] ph -> ( rank ` x ) C_ ( rank ` y ) ) ) } =/= (/) ) $=
      ( vz cab c0 wne cv crnk cfv wss wi wal wa wceq wral crab wcel ax-17 bitri
      wex wsbc abn0 scott0 hbab1 hbral sseq1d ralbidv cbvrab df-rab abid df-ral
      fveq2 df-clab imbi1i albii anbi12i abbii 3eqtri eqeq1i necon3bii bitr3i )
      ABUAABEZFGAABCHZUBZBHZIJZVDIJZKZLZCMZNZBEZFGABUCVCFVMFVCFODHZIJZVHKZCVCPZ
      DVCQZFOVMFODCVCUDVRVMFVRVICVCPZBVCQVFVCRZVSNZBEVMVQVSDBCVCVDVCRZDSABCUEZV
      PBCVCWCVPBSUFVSDSVNVFOZVPVICVCWDVOVGVHVNVFIUMUGUHUIVSBVCUJWAVLBVTAVSVKABU
      KVSWBVILZCMVKVICVCULWEVJCWBVEVIACBUNUOUPTUQURUSUTTVAVB $.
      $( [6-Apr-2007] $) $( [13-Oct-2003] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.  $d w C $.  $d w D $.
    cplem1.1 $e |- C = { y e. B | A. z e. B ( rank ` y ) C_ ( rank ` z ) } $.
    cplem1.2 $e |- D = U_ x e. A C $.
    $( Lemma for the Collection Principle ~ cp . $)
    cplem1 $p |- A. x e. A ( B =/= (/) -> ( B i^i D ) =/= (/) ) $=
      ( vw c0 wne cin wi cv wcel wex wceq crnk cfv wral scott0 eqeq1i necon3bii
      wss crab bitr4i n0 bitri wa ssrab2 eqsstri sseli a1i ciun syl6sseqr sseld
      ssiun2 jcad inelcm syl6 exlimdv syl5bi rgen ) EKLZEGMKLZNADVEJOZFPZJQZAOD
      PZVFVEFKLVIEKFKEKRBOSTCOSTUECEUAZBEUFZKRFKRBCEUBFVLKHUCUGUDJFUHUIVJVHVFJV
      JVHVGEPZVGGPZUJVFVJVHVMVNVHVMNVJFEVGFVLEHVKBEUKULUMUNVJFGVGVJFADFUOGADFUR
      IUPUQUSVGEGUTVAVBVCVD $.
      $( [2-Apr-2007] $) $( [17-Oct-2003] $)
  $}

  ${
    $d x y z w A $.  $d y z w B $.
    cplem2.1 $e |- A e. _V $.
    $( -Lemma for the Collection Principle ~ cp . $)
    cplem2 $p |- E. y A. x e. A ( B =/= (/) -> ( B i^i y ) =/= (/) ) $=
      ( vz vw c0 wne cv crnk cfv wss wral crab ciun cin wi wex eqid cplem1 wceq
      scottex iunex hbiu1 hbeleq ineq2 neeq1d imbi2d ralbid cla4ev ax-mp ) DHIZ
      DACFJKLGJKLMGDNFDOZPZQZHIZRZACNZUMDBJZQZHIZRZACNZBSAFGCDUNUOUNTUOTUAVDUSB
      UOACUNEFGDUCUDUTUOUBZVCURACABUOABCUNUEUFVEVBUQUMVEVAUPHUTUODUGUHUIUJUKUL
      $.
      $( [2-Apr-2007] $) $( [17-Oct-2003] $)
  $}

  ${
    $d ph z w $.  $d x y z w $.
    $( Collection Principle.  This remarkable theorem scheme is in effect a
       very strong generalization of the Axiom of Replacement.  The proof makes
       use of Scott's trick ~ scottex that collapses a proper class into a set
       of minimum rank.  The wff ` ph ` can be thought of as
       ` ph ( x , y ) ` .  Scheme "Collection Principle" of [Jech] p. 72. $)
    cp $p |- E. w A. x e. z ( E. y ph -> E. y e. w ph ) $=
      ( cab c0 wne cv cin wi wral wex wrex vex cplem2 abn0 wcel wa exbii anbi1i
      elin abid ancom 3bitri hbab1 ax-17 hbin n0f df-rex 3bitr4i imbi12i ralbii
      mpbi ) ACFZGHZUOEIZJZGHZKZBDIZLZEMACMZACUQNZKZBVALZEMBEVAUODOPVBVFEUTVEBV
      AUPVCUSVDACQCIZURRZCMVGUQRZASZCMUSVDVHVJCVHVGUORZVISAVISVJVGUOUQUBVKAVIAC
      UCUAAVIUDUETCDURCDUOUQACDUFVAUQRCUGUHUIACUQUJUKULUMTUN $.
      $( [17-Oct-2003] $)
  $}

  ${
    $d ph z w $.  $d x y z w $.
    $( A very strong generalization of the Axiom of Replacement (compare
       ~ zfrep6 ), derived from the Collection Principle ~ cp .  Its strength
       lies in the rather profound fact that ` ph ( x , y ) ` does not have to
       be a "function-like" wff, as it does in the standard Axiom of
       Replacement.  This theorem is sometimes called the Boundedness Axiom. $)
    bnd $p |- ( A. x e. z E. y ph -> E. w A. x e. z E. y e. w ph ) $=
      ( wex cv wral wrex wi cp ralim eximi ax-mp 19.37v mpbi ) ACFZBDGZHZACEGIZ
      BRHZJZEFZSUAEFJQTJBRHZEFUCABCDEKUDUBEQTBRLMNSUAEOP $.
      $( [2-Jan-2004] $) $( [17-Oct-2004] $)
  $}

  ${
    $d ph z w v $.  $d x z w v A $.  $d x y z w v B $.
    bnd2.1 $e |- A e. _V $.
    $( A variant of the Boundedness Axiom ~ bnd that picks a subset ` z ` out
       of a possibly proper class ` B ` in which a property is true. $)
    bnd2 $p |- ( A. x e. A E. y e. B ph ->
              E. z ( z C_ B /\ A. x e. A E. y e. z ph ) ) $=
      ( vw vv wrex wral cv wcel wa wex wss df-rex wi wceq raleq bnd vtocl sylbi
      ralbii exbidv imbi12d cin inex1 inss2 sseq1 mpbiri biantrurd rexeq anbi1i
      vex elin anass bitri rexbii2 syl6bb ralbidv bitr3d cla4ev exlimiv syl ) A
      CFJZBEKZCLZFMZANZCHLZJZBEKZHOZDLZFPZACVOJZBEKZNZDOZVGVJCOZBEKZVNVFWABEACF
      QUDWABILZKZVLBWCKZHOZRWBVNRIEGWCESZWDWBWFVNWABWCETWGWEVMHVLBWCETUEUFVJBCI
      HUAUBUCVMVTHVSVMDVKFUGZVKFHUOUHVOWHSZVRVSVMWIVPVRWIVPWHFPVKFUIVOWHFUJUKUL
      WIVQVLBEWIVQACWHJVLACVOWHUMAVJCWHVKVHWHMZANVHVKMZVINZANWKVJNWJWLAVHVKFUPU
      NWKVIAUQURUSUTVAVBVCVDVE $.
      $( [4-Feb-2004] $)
  $}

  ${
    $d x y z A $.
    $( The collection of all sets equinumerous to a set ` A ` and having least
       possible rank is a set.  This is the part of the justification of the
       definition of kard of [Enderton] p. 222. $)
    kardex $p |- { x | ( x ~~ A /\
                  A. y ( y ~~ A -> ( rank ` x ) C_ ( rank ` y ) ) ) } e. _V $=
      ( vz cv crnk cfv wss cen wbr cab wral crab wi wal cvv wcel df-rab breq1
      wa vex elab ralab anbi12i abbii eqtri scottex eqeltrri ) AEZFGBEZFGHZBDEZ
      CIJZDKZLZAUNMZUICIJZUJCIJZUKNBOZTZAKZPUPUIUNQZUOTZAKVAUOAUNRVCUTAVBUQUOUS
      UMUQDUIAUAULUICISUBUMURUKBDULUJCISUCUDUEUFABUNUGUH $.
      $( [21-Mar-2007] $) $( [14-Dec-2003] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d z C $.  $d z D $.
    karden.1 $e |- A e. _V $.
    karden.2 $e |- B e. _V $.
    karden.3 $e |- C =
       { x | ( x ~~ A /\ A. y ( y ~~ A -> ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    karden.4 $e |- D =
       { x | ( x ~~ B /\ A. y ( y ~~ B -> ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    $( If we allow the Axiom of Regularity, we can avoid the Axiom of Choice by
       defining the cardinal number of a set as the set of all sets
       equinumerous to it and having least possible rank.  This theorem proves
       the equinumerosity relationship for this definition (compare
       ~ carden ).  The hypotheses correspond to the definition of kard of
       [Enderton] p. 222 (which we don't define separately since currently we
       do not use it elsewhere).  This theorem along with ~ kardex justify the
       definition of kard.  The restriction to least rank prevents the proper
       class that would result from ` { x | x ~~ A } ` . $)
    karden $p |- ( C = D <-> A ~~ B ) $=
      ( vz vw cen wbr cv crnk breq1 wa wi wal wceq cfv wss cab wral wrex c0 wne
      crab wex enref cla4ev ax-mp abn0 mpbir necon3bii mpbi rabn0 wcel vex elab
      scott0 ralab anbi12i simpl a1i wb eqeq12i abbi bitr4i fveq2 sseq1d imbi2d
      albidv anbi12d bibi12d a4v sylbi syl6bi jcad ensym entr sylan syl6 syl5bi
      exp3a rexlimdv mpi cvv enen2 mpan imbi1d abbidv 3eqtr4g impbii ) EFUAZCDM
      NZWPKOZPUBZBOZPUBZUCZBLOZCMNZLUDZUEZKXEUFZWQXFKXEUIZUGUHZXGXEUGUHZXIXJXDL
      UJZCCMNZXKCGUKXDXLLCGXCCCMQULUMXDLUNUOXEUGXHUGKBXEVBUPUQXFKXEURUQWPXFWQKX
      EWPWRXEUSZXFWQXMXFRWRCMNZWTCMNZXBSZBTZRZWPWQXMXNXFXQXDXNLWRKUTXCWRCMQVAXD
      XOXBBLXCWTCMQVCVDWPXRXNWRDMNZRWQWPXRXNXSXRXNSWPXNXQVEVFWPXRXSWTDMNZXBSZBT
      ZRZXSWPAOZCMNZXOYDPUBZXAUCZSZBTZRZYDDMNZXTYGSZBTZRZVGZATZXRYCVGZWPYJAUDZY
      NAUDZUAYPEYRFYSIJVHYJYNAVIVJYOYQAKYDWRUAZYJXRYNYCYTYEXNYIXQYDWRCMQYTYHXPB
      YTYGXBXOYTYFWSXAYDWRPVKVLZVMVNVOYTYKXSYMYBYDWRDMQYTYLYABYTYGXBXTUUAVMVNVO
      VPVQVRXSYBVEVSVTXNCWRMNXSWQWRCGWACWRDWBWCWDWEWFWGWHWQYRYSEFWQYJYNAWQYEYKY
      IYMDWIUSZWQYEYKVGHCDYDWIWJWKWQYHYLBWQXOXTYGUUBWQXOXTVGHCDWTWIWJWKWLVNVOWM
      IJWNWO $.
      $( [18-Dec-2003] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    htalem.1 $e |- A e. _V $.
    htalem.2 $e |- B = U. { x e. A | A. y e. A -. y R x } $.
    $( Lemma for defining an emulation of Hilbert's epsilon.  Hilbert's epsilon
       is described at ~ http://plato.stanford.edu/entries/epsilon-calculus/ .
       This theorem is equivalent to Hilbert's "transfinite axiom," described
       on that page, with the additional ` R We A ` antecedent.  The element
       ` B ` is the epsilon that the theorem emulates. $)
    htalem $p |- ( ( R We A /\ A =/= (/) ) -> B e. A ) $=
      ( wwe c0 wne wa cv wbr wn wral crab cuni wss wcel ssid wereucl syl5eqel
      mp3an2 ) CEHZCIJZKDBLALEMNBCOACPQZCGUDCCRUEUFCSCTABCCEFUAUCUB $.
      $( [10-Apr-2007] $) $( [11-Mar-2004] $)
  $}

  ${
    $d z v A $.  $d x y R z v $.  $d y z v ph $.
    hta.1 $e |- A = { x | ( ph /\ A. y ( [ y / x ] ph ->
                                         ( rank ` x ) C_ ( rank ` y ) ) ) } $.
    hta.2 $e |- B = U. { x e. A | A. y e. A -. y R x } $.
    $( A ZFC emulation of Hilbert's transfinite axiom.  The set ` B ` has the
       properties of Hilbert's epsilon, except that it also depends on a
       well-ordering ` R ` .  This theorem arose from discussions with Raph
       Levien on 5-Mar-2004 about translating the HOL proof language, which
       uses Hilbert's epsilon.  See
       ~ http://us.metamath.org/downloads/choice.txt (copy of obsolete link
       http://ghilbert.org/choice.txt) and
       ~ http://us.metamath.org/downloads/megillaward2005he.pdf .

       Hilbert's epsilon is described at
       ~ http://plato.stanford.edu/entries/epsilon-calculus/ .  This theorem
       differs from Hilbert's transfinite axiom described on that page in that
       it requires ` R We A ` as an antecedent.  Class ` A ` collects the sets
       of least rank for which ` ph ( x ) ` is true.  Class ` B ` , which
       emulates the epsilon, is the minimum element in a well-ordering ` R ` on
       ` A ` .

       If a well-ordering ` R ` on ` A ` can be expressed in a closed form, as
       might be the case if we are working with say natural numbers, we can
       eliminate the antecedent with modus ponens, giving us the exact
       equivalent of Hilbert's transfinite axiom.  Otherwise, we replace ` R `
       with a dummy set variable, say ` w ` , and attach ` w We A ` as an
       antecedent in each step of the ZFC version of the HOL proof until the
       epsilon is eliminated.  At that point, ` B ` (which will have ` w ` as a
       free variable) will no longer be present, and we can eliminate
       ` w We A ` by applying ~ exlimiv and ~ weth , using ~ scottexs to
       establish the existence of ` A ` .

       For a version of this theorem scheme using class (meta)variables instead
       of wff (meta)variables, see ~ htalem . $)
    hta $p |- ( R We A -> ( ph -> [ B / x ] ph ) ) $=
      ( vz vv cv wcel wceq wb ax-mp wbr wn wral crab ax-17 wsbc crnk cfv wss wi
      wal wa cab wne wwe 19.8a scott0s sylib weeq2 scottexs cuni hba1 hban hbab
      c0 wex hbxfreq raleqf a1i rabbiia hbab1 rabeqf hbral breq2 notbid ralbidv
      cbvrab breq1 cbvralf eqtri 3eqtri unieqi htalem ex sylbi simpl ss2abi cvv
      sseli eqeltri hbel ssrab2 eqsstri ssexi uniex elabs syl56 ) AAABCKZUABKZU
      BUCWMUBUCUDUEZCUFZUGZBUHZUTUIZDFUJZEWRLZABEUAZAABVAWSABUKABCULUMWTWRFUJZW
      SXAUEDWRMZWTXCNGDWRFUNOXCWSXAIJWREFABCUOZEWMWNFPZQZCDRZBDSZUPZJKZIKZFPZQZ
      JWRRZIWRSZUPHXIXPXIXGCWRRZBDSZXQBWRSZXPXHXQBDXHXQNZWNDLXDXTGXGCIDWRCIDWRG
      WQCBIAWPCACTWOCUQURUSZVBYAVCOVDVEXDXRXSMGXQBIDWRBIDWRGWQBIVFZVBZYBVGOXSWM
      XLFPZQZCWRRZIWRSXPXQYFBIJWRWQBJVFXKWRLITXQITYEBCWRWQBCVFYEBTZVHWNXLMZXGYE
      CWRYHXFYDWNXLWMFVIVJZVKVLYFXOIWRYFXONXLWRLZYEXNCJIWRYAYJJTYEJTXNCTWMXKMYD
      XMWMXKXLFVMVJVNVDVEVOVPVQVOVRVSVTXAEABUHZLXBWRYKEWQABAWPWAWBWDABEEXJWCHXI
      XIDDWRWCGXEWEXIYECDRZIDSDXHYLBIJDBIIXKDXLXKLBTYCWFXKDLITXHITYEBCDBIIWMDXL
      WMLBTYCWFYGVHYHXGYECDYIVKVLYLIDWGWHWIWJWEWKUMWL $.
      $( [11-Mar-2004] $)

  $}

