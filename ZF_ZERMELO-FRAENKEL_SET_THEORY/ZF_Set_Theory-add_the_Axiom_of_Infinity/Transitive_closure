$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Transitive closure
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)


  ${
    $d x y z w v u $.  $d y z v A $.  $d y v u F $.
    trcl.1 $e |- A e. _V $.
    trcl.2 $e |- F =
               ( rec ( { <. z , w >. | w = ( z u. U. z ) } , A ) |` om ) $.
    trcl.3 $e |- C = U_ y e. om ( F ` y ) $.
    $( For any set ` A ` , show the properties of its transitive closure
       ` C ` .  Similar to Theorem 9.1 of [TakeutiZaring] p. 73 except that we
       show an explicit expression for the transitive closure rather than just
       its existence.  See ~ tz9.1 for an abbreviated version showing
       existence. $)
    trcl $p |- ( A C_ C /\ Tr C /\ A. x ( ( A C_ x /\ Tr x ) -> C C_ x ) ) $=
      ( vv vu wss cv com cfv c0 wcel wceq fveq2 wtr wa wi ciun wrex peano1 cuni
      wal cun copab crdg cres fveq1i cvv fr0g ax-mp eqtr2i eqimssi sseq2d mp2an
      rcla4ev ssiun sseqtr4i dftr2 csuc eliun anbi2i r19.42v bitr4i elunii fvex
      ssun2 uniex unex ax-17 hbopab1 hbrdg hbres hbxfreq hbfv hbuni hbun uneq12
      unieq mpdan frsucopab mpan2 syl5sseqr sseld syl5 reximia peano2 eleq2d ex
      sylbi syl rexlimiv cbvrexv sylibr ax-gen mpgbir wb treq mpbir wral sseq1d
      eqtri sseq1i biimpri adantr uniss df-tr syl5bi anc2li unss syl6ib biimprd
      sstr2 syl9r com23 adantld finds2 com12 ralrimiv cbviunv iunss 3pm3.2i
      bitri ) EFMFUAZEANZMZYJUAZUBZFYJMZUCZAUHEBOBNZGPZUDZFEYQMZBOUEZEYRMQOREQG
      PZMZYTUFEUUAUUAQDNCNZUUCUGZUIZSZCDUJZEUKZOULZPZEQGUUIIUMZEUNRUUJESHEUNUUG
      UOUPZUQURYSUUBBQOYPQSYQUUAEYPQGTUSVAUTBOYQEVBUPJVCYIYRUAZUUMKNZLNZRZUUOYR
      RZUBZUUNYRRZUCZLUHKKLYRVDUUTLUURUUNYPVEZGPZRZBOUEZUUSUURUUPUUOYQRZUBZBOUE
      ZUVDUURUUPUVEBOUEZUBUVGUUQUVHUUPBUUOOYQVFVGUUPUVEBOVHVIUVFUVCBOUVFUUNYQUG
      ZRYPORZUVCUUNUUOYQVJUVJUVIUVBUUNUVJYQUVIUIZUVIUVBUVIYQVLUVJUVKUNRUVBUVKSY
      QUVIYPGVKZYQUVLVMVNCDKEYPUUEUVKUNGUUNERCVOZUUNYPRCVOZCKYQUVICKYPGCKGUUIIC
      KUUHOCKEUUGUUFCDKVPUVMVQUUNORZCVOVRVSUVNVTZCKYQUVPWAWBIUUCYQSUUDUVISUUEUV
      KSUUCYQWDUUCYQUUDUVIWCWEWFWGZWHWIWJWKWOUVDUUNYQRZBOUEZUUSUVDUUNUUOGPZRZLO
      UEZUVSUVCUWBBOUVJUVAORZUVCUWBUCYPWLUWCUVCUWBUWAUVCLUVAOUUOUVASUVTUVBUUNUU
      OUVAGTWMVAWNWPWQUVRUWABLOYPUUOSYQUVTUUNYPUUOGTWMWRWSBUUNOYQVFWSWPWTXAFYRS
      YIUUMXBJFYRXCUPXDYOAYMUUNGPZYJMZKOXEZYNYMUWEKOUVOYMUWEUWEUUAYJMZYQYJMZUVB
      YJMZYMKBUUNQSUWDUUAYJUUNQGTXFUUNYPSUWDYQYJUUNYPGTXFUUNUVASUWDUVBYJUUNUVAG
      TXFYKUWGYLUWGYKUUAEYJUUAUUJEUUKUULXGXHXIXJUVJYLUWHUWIUCYKUVJUWHYLUWIUWHYL
      UVKYJMZUVJUWIUWHYLUWHUVIYJMZUBUWJUWHYLUWKUWHUVIYJUGZMZYLUWKUCYQYJXKYLUWLY
      JMUWMUWKYJXLUVIUWLYJXRXMWPXNYQUVIYJXOXPUVJUWIUWJUVJUVBUVKYJUVQXFXQXSXTYAY
      BYCYDYNKOUWDUDZYJMUWFFUWNYJFYRUWNJBKOYQUWDYPUUNGTYEXGXHKOUWDYJYFYHWSWTYG
      $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d A x y z w v $.
    tz9.1.1 $e |- A e. _V $.
    $( Every set has a transitive closure (smallest transitive extension).
       Theorem 9.1 of [TakeutiZaring] p. 73.  See ~ trcl for an explicit
       expression for the transitive closure.  Apparently open problems are
       whether this theorem can be proved without the Axiom of Infinity; if
       not, then whether it implies Infinity; and if not, what is the
       "property" that Infinity has that the other axioms don't have that is
       weaker than Infinity itself?

       (Added 22-Mar-2011) The following article seems to answer the first
       question, that it can't be proved without Infinity, in the affirmative:
       Mancini, Antonella and Zambella, Domenico (2001).  "A note on recursive
       models of set theories." _Notre Dame Journal of Formal Logic_,
       42(2):109-115.  (Thanks to Scott Fenton.) $)
    tz9.1 $p |- E. x ( A C_ x /\ Tr x
                /\ A. y ( ( A C_ y /\ Tr y ) -> x C_ y ) ) $=
      ( vz vv vw com cv cuni cun wceq copab crdg wss wtr wi wal w3a eqid cfv wa
      cres ciun trcl omex fvex iunex sseq2 sseq1 imbi2d albidv 3anbi123d cla4ev
      wex treq ax-mp ) CEHEIZFIGIZUSJKLGFMCNHUCZUAZUDZOZVBPZCBIZOVEPUBZVBVEOZQZ
      BRZSZCAIZOZVKPZVFVKVEOZQZBRZSZAUOBEGFCVBUTDUTTVBTUEVQVJAVBEHVAUFURUTUGUHV
      KVBLZVLVCVMVDVPVIVKVBCUIVKVBUPVRVOVHBVRVNVGVFVKVBVEUJUKULUMUNUQ $.
      $( [22-Mar-2011] $) $( [15-Sep-2003] $)

    $( Alternative expression for the existence of transitive closures
       ~ tz9.1 : the intersection of all transitive sets containing ` A ` is a
       set.  (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    tz9.1c $p |- |^| { x | ( A C_ x /\ Tr x ) } e. _V $=
      ( vw vz vy cv wss wtr wa cab c0 wne cint cvv wcel wex com wceq eqid copab
      cuni cun crdg cres cfv ciun wi wal trcl 3simpa omex fvex iunex sseq2 treq
      w3a anbi12d cla4ev mp2b abn0 mpbir intex mpbi ) BAGZHZVEIZJZAKZLMZVINOPVJ
      VHAQZBDRDGZEGFGZVMUBUCSFEUABUDRUEZUFZUGZHZVPIZVHVPVEHUHAUIZUQVQVRJZVKADFE
      BVPVNCVNTVPTUJVQVRVSUKVHVTAVPDRVOULVLVNUMUNVEVPSVFVQVGVRVEVPBUOVEVPUPURUS
      UTVHAVAVBVIVCVD $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x y z w A $.
    $( The strong form of the Axiom of Regularity (no sethood requirement on
       ` A ` ), with the axiom itself present as an antecedent.  See also
       ~ zfregs .  (Contributed by Mario Carneiro, 22-Mar-2013.) $)
    epfrs $p |- ( ( _E Fr A /\ A =/= (/) ) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( vz vy vw c0 wne cv cin wceq wrex wcel wex wi n0 wss wtr wa wel syl5 cep
      wfr csn wal w3a snex tz9.1 snssi anim2i ssin snss bitr4i sylib ne0i inss2
      vex syl inex1 epfrc mp3an2 elin anbi1i anass bitri inss1 sseli ancri trel
      inass incom ineq2i eqtri eleq2i bitr2i sylbi ex exp3a com34 imp3a exlimdv
      syl6 syl5bi imp necon4d anim2d expimpd reximdv2 exp3acom23 impcom 3adant3
      com23 exlimiv ax-mp ) BFGZBUAUBZAHZBIZFJZABKZWNCHZBLZCMWOWSNZCBOXAXBCWTUC
      ZDHZPZXDQZXCEHZPXGQRXDXGPNEUDZUEZDMXAXBNZDEXCWTUFUGXIXJDXEXFXJXHXFXEXJXFX
      EXAXBXEXARZXDBIZFGZXFXBXKWTXLLZXMXKXEXCBPZRZXNXAXOXEWTBUHUIXPXCXLPXNXCXDB
      UJWTXLCUPUKULUMXLWTUNUQXFWOXMWSWOXMRXLWPIZFJZAXLKZXFWSWOXLBPXMXSXDBUOABXL
      XDBDUPURUSUTXFXRWRAXLBWPXLLZXRRZADSZWPBLZXRRZRZXFYCWRRZYAYBYCRZXRRYEXTYGX
      RWPXDBVAVBYBYCXRVCVDXFYBYDYFXFYBRZXRWRYCYHWQFXQFXFYBWQFGZXQFGZNXFYIYBYJYI
      XGWQLZEMXFYBYJNZEWQOXFYKYLEYKEASZYKRXFYLYKYMWQWPXGWPBVEVFVGXFYMYKYLXFYMYB
      YKYJXFYMYBYKYJNZXFYMYBREDSZYNXDXGWPVHYOYKYJYOYKRZXGXQLZYJYQXGXDWQIZLYPXQY
      RXGXQXDBWPIZIYRXDBWPVIYSWQXDBWPVJVKVLVMXGXDWQVAVNXQXGUNVOVPWAVQVRVSTVTWBW
      KWCWDWEWFWBWGTWHTVQWIWJWLWMWLVOWI $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x A $.
    $( The strong form of the Axiom of Regularity, which does not require that
       ` A ` be a set.  Axiom 6' of [TakeutiZaring] p. 21.  See also
       ~ epfrs . $)
    zfregs $p |- ( A =/= (/) -> E. x e. A ( x i^i A ) = (/) ) $=
      ( cep wfr c0 wne cv cin wceq wrex zfregfr epfrs mpan ) BCDBEFAGBHEIABJBKA
      BLM $.
      $( [12-Apr-2007] $) $( [17-Sep-2003] $)
  $}

  ${
    $d x y A $.
    $( Alternate strong form of the Axiom of Regularity.  Not every element of
       a non-empty class contains some element of that class.  (Contributed by
       Alan Sare, 24-Oct-2011.)  (The proof was shortened by Wolf Lammen,
       27-Sep-2013.) $)
    zfregs2 $p |- ( A =/= (/) -> -. A. x e. A E. y ( y e. A /\ y e. x ) ) $=
      ( c0 wne cv wcel wa wex wn wral wrex wi wal cin zfregs incom rexbii sylib
      wceq eqeq1i disj1 alinexa dfrex2 notnot ralbii sylnibr ) CDEZBFZCGZUIAFZG
      ZHBIZJZJZACKZUMACKUHUNACLZUPJUHUJULJMBNZACLZUQUHCUKOZDTZACLZUSUHUKCOZDTZA
      CLVBACPVDVAACVCUTDUKCQUARSVAURACBCUKUBRSURUNACUJULBUCRSUNACUDSUMUOACUMUEU
      FUG $.
      $( [27-Sep-2013] $) $( [24-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Lemma for ~ en3lp . $)
    en3lplem1 $p |- ( ( A e. B /\ B e. C /\ C e. A ) ->
                    ( x = A -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel w3a cv wceq wa simp3 eleq2 syl5ibrcom tpid3g 3ad2ant3 elex22 sylan
      ctp wex ex syld ) CDFZDEFZECFZGZAHZCIZEUFFZBHZCDERZFUIUFFJBSZUEUHUGUDUBUC
      UDKUFCELMUEUHUKUEEUJFZUHUKUDUBULUCECCDNOBEUJUFPQTUA $.
      $( [28-Oct-2011] $)

    $( Lemma for ~ en3lp . $)
    en3lplem2 $p |- ( ( A e. B /\ B e. C /\ C e. A ) -> ( x e. { A , B , C }
                    -> E. y ( y e. { A , B , C } /\ y e. x ) ) ) $=
      ( wcel cv ctp wa wex wceq wo en3lplem1 a1d tprot eleq2i anbi1i imp syl6ib
      wi w3a wel 3comr exbii bicomi syl8ib jao sylsyld 3coml w3o cab dftp2 abid
      idd df-3or syl3c ex ) CDFZDEFZECFZUAZAGZCDEHZFZBGZVCFZBAUBZIZBJZVAVDIVBCK
      ZVBDKZLZVITZVBEKZVITZVLVNLZVIVAVDVMVAVJVITVDVKVITVMABCDEMVAVDVKVEDECHZFZV
      GIZBJZVIVAVKVTTZVDUSUTURWAABDECMUCNVIVTVHVSBVFVRVGVCVQVECDEOPQUDUEUFVJVIV
      KUGUHRVAVDVOVAVDVNVEECDHZFZVGIZBJZVIVAVNWETZVDUTURUSWFABECDMUINWDVHBWCVFV
      GWBVCVEECDOPQUDUFRVAVDVPVAVDVJVKVNUJZVPVAVDVBWGAUKZFZWGVAVDVDWIVAVDUNVCWH
      VBACDEULPSWGAUMSVJVKVNUOSRVLVIVNUGUPUQ $.
      $( [28-Oct-2011] $)

    $( No class has 3-cycle membership loops.  This proof was automatically
       generated from the virtual deduction proof ~ en3lpVD using a translation
       program.  (Contributed by Alan Sare, 24-Oct-2011.) $)
    en3lp $p |- -. ( A e. B /\ B e. C /\ C e. A ) $=
      ( vy vx wcel w3a wn ctp c0 wceq noel eleq2 mtbiri tpid3g simp3 wne cv wel
      nsyl wa wex wral zfregs2 en3lplem2 ralrimiv pm2.61ine ) ABFZBCFZCAFZGZHAB
      CIZJULJKZUJUKUMCULFZUJUMUNCJFCLULJCMNCAABOTUHUIUJPTULJQDRULFDESUADUBZEULU
      CUKEDULUDUKUOEULEDABCUEUFTUG $.
      $( [28-Oct-2011] $)
  $}

  ${
    $d x y z A $.
    $( Set (epsilon) induction.  Theorem 5.22 of [TakeutiZaring] p. 21. $)
    setind $p |- ( A. x ( x C_ A -> x e. A ) -> A = _V ) $=
      ( vy cv wss wcel wi wal cvv cdif c0 wceq cin wrex wn ssindif0 sseq1 eleq1
      imbi12d a4v syl5bir eldifn nsyli imp nrexdv zfregs necon1bi vdif0 sylibr
      syl ) ADZBEZUKBFZGZAHZIBJZKLZBILUOCDZUPMKLZCUPNZOUQUOUSCUPUOURUPFZUSOUOUS
      URBFZVAUSURBEZUOVBURBPUNVCVBGACUKURLULVCUMVBUKURBQUKURBRSTUAURIBUBUCUDUEU
      TUPKCUPUFUGUJBUHUI $.
      $( [17-Sep-2003] $)
  $}

  ${
    $d x A $.
    $( Set (epsilon) induction, stated compactly.  Given as a homework problem
       in 1992 by George Boolos (1940-1996). $)
    setind2 $p |- ( ~P A C_ A -> A = _V ) $=
      ( vx cpw wss cv wcel wi wal cvv wceq pwss setind sylbi ) ACADBEZADNAFGBHA
      IJBAAKBALM $.
      $( [17-Sep-2003] $)
  $}

  $c TC $. $( Transitive closure $)

  $( Extend class notation to include the transitive closure function. $)
  ctc $a class TC $.

  ${
    $d u x y $.
    $( The transitive closure function. $)
    df-tc $a |- TC = ( x e. _V |-> |^| { y | ( x C_ y /\ Tr y ) } ) $.
  $}

  ${
    $d A x y $.
    $( Value of the transitive closure function.  (The fact that this
       intersection exists is a non-trivial fact that depends on ~ ax-inf ; see
       ~ tz9.1 .)  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcvalg $p |- ( A e. V -> ( TC ` A ) = |^| { x | ( A C_ x /\ Tr x ) } ) $=
      ( vy cv ctc cfv wss wtr wa cab cint wceq fveq2 sseq1 anbi1d abbidv inteqd
      cvv wcel eqeq12d vex tz9.1c df-tc fvmpt2 mp2an vtoclg ) DEZFGZUHAEZHZUJIZ
      JZAKZLZMZBFGZBUJHZULJZAKZLZMDBCUHBMZUIUQUOVAUHBFNVBUNUTVBUMUSAVBUKURULUHB
      UJOPQRUAUHSTUOSTUPDUBZAUHVCUCDSUOSFDAUDUEUFUG $.
      $( [23-Jun-2013] $)
  $}

  ${
    $d A x y $.
    $( Defining property of the transitive closure function: it contains its
       argument as a subset.  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcid $p |- ( A e. V -> A C_ ( TC ` A ) ) $=
      ( vx wcel cv wss wtr wa cab cint ctc cfv ssmin tcvalg syl5sseqr ) ABDACEZ
      FPGZHCIJAAKLQCAMCABNO $.
      $( [23-Jun-2013] $)

    $( Defining property of the transitive closure function: it is transitive.
       (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tctr $p |- Tr ( TC ` A ) $=
      ( vx vy cvv wcel ctc cfv wtr cv wss wa cab cint trint wceq treq wb mpbiri
      syl c0 vex sseq2 anbi12d elab simprbi mprg tcvalg wn tr0 fvprc pm2.61i )
      ADEZAFGZHZULUNABIZJZUOHZKZBLZMZHZCIZHZVACUSCUSNVBUSEAVBJZVCURVDVCKBVBCUAU
      OVBOUPVDUQVCUOVBAUBUOVBPUCUDUEUFULUMUTOUNVAQBADUGUMUTPSRULUHZUNTHZUIVEUMT
      OUNVFQAFUJUMTPSRUK $.
      $( [23-Jun-2013] $)

    $d B x y $.
    $( Defining property of the transitive closure function: it is a subset of
       any transitive class containing ` A ` .  (Contributed by Mario Carneiro,
       23-Jun-2013.) $)
    tcmin $p |- ( A e. V -> ( ( A C_ B /\ Tr B ) -> ( TC ` A ) C_ B ) ) $=
      ( vy vx wcel wss wtr wa cv cab cint ctc cfv wex cvv tcvalg fvex syl6eqelr
      intexab sylibr ssin biimpi trin anim12i an4s expcom inex1 wceq sseq2 treq
      cin vex anbi12d intss1 sylbir inss2 syl6ss exlimdv syl5com sseq1d sylibrd
      elab syl6 ) ACFZABGZBHZIZADJZGZVIHZIZDKZLZBGZAMNZBGVEAEJZGZVQHZIZEOZVHVOV
      EVTEKLZPFWAVEWBVPPEACQAMRSVTETUAVHVTVOEVHVTAVQBULZGZWCHZIZVOVTVHWFVRVFVSV
      GWFVRVFIZWDVSVGIWEWGWDAVQBUBUCVQBUDUEUFUGWFVNWCBWFWCVMFVNWCGVLWFDWCVQBEUM
      UHVIWCUIVJWDVKWEVIWCAUJVIWCUKUNVCWCVMUOUPVQBUQURVDUSUTVEVPVNBDACQVAVB $.
      $( [23-Jun-2013] $)
  $}

  ${
    $d A x y $.
    tc2.1 $e |- A e. _V $.
    $( A variant of the definition of the transitive closure function, using
       instead the smallest transitive set containing ` A ` as a member, gives
       almost the same set, except that ` A ` itself must be added because it
       is not usually a member of ` ( TC `` A ) ` (and it is never a member if
       ` A ` is well-founded).  (Contributed by Mario Carneiro,
       23-Jun-2013.) $)
    tc2 $p |- ( ( TC ` A ) u. { A } ) = |^| { x | ( A e. x /\ Tr x ) } $=
      ( ctc cfv cun wcel wtr wa cab cint wss wceq ax-mp eqsstri mpbi unssi cuni
      cvv df-tr csn cv tcvalg trss imdistanri ss2abi intss elintab simpl mpgbir
      wi snss snid elun2 uniun tctr unisn tcid ssun1 sstri mpbir fvex snex unex
      eleq2 treq anbi12d elab mpbir2an intss1 eqssi ) BDEZBUAZFZBAUBZGZVOHZIZAJ
      ZKZVLVMVTVLBVOLZVQIZAJZKZVTBSGZVLWDMCABSUCNVSWCLWDVTLVRWBAVQVPWAVOBUDUEUF
      VSWCUGNOBVTGZVMVTLWFVRVPUKAVRABCUHVPVQUIUJBVTCULPQVNVSGZVTVNLWGBVNGZVNHZB
      VMGWHBCUMBVMVLUNNWIVNRZVNLWJVLVNWJVLRZVMRZFVLVLVMUOWKWLVLVLHWKVLLBUPVLTPW
      LBVLBCUQWEBVLLCBSURNOQOVLVMUSUTVNTVAVRWHWIIAVNVLVMBDVBBVCVDVOVNMVPWHVQWIV
      OVNBVEVOVNVFVGVHVIVNVSVJNVK $.
      $( [23-Jun-2013] $)

    $d B x $.
    $( The transitive closure function inherits the subset relation.
       (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcss $p |- ( B C_ A -> ( TC ` B ) C_ ( TC ` A ) ) $=
      ( vx wss ctc cfv cv wtr wa cab cint cvv wcel wceq tcvalg syl sstr2 anim1d
      ssex ss2abdv intss ax-mp syl6sseqr eqsstrd ) BAEZBFGZBDHZEZUHIZJZDKZLZAFG
      ZUFBMNUGUMOBACTDBMPQUFUMAUHEZUJJZDKZLZUNUFUQULEUMUREUFUPUKDUFUOUIUJBAUHRS
      UAUQULUBQAMNUNUROCDAMPUCUDUE $.
      $( [23-Jun-2013] $)

    $( The transitive closure function converts the element relation to the
       subset relation.  (Contributed by Mario Carneiro, 23-Jun-2013.) $)
    tcel $p |- ( B e. A -> ( TC ` B ) C_ ( TC ` A ) ) $=
      ( vx wcel ctc cfv cv wss wtr wa cab cint tcvalg wi ssel com12 syl6com cvv
      trss imp3a simpr a1i jcad ss2abdv intss syl wceq ax-mp syl6sseqr eqsstrd
      ) BAEZBFGBDHZIZUMJZKZDLZMZAFGZDBANULURAUMIZUOKZDLZMZUSULVBUQIURVCIULVAUPD
      ULVAUNUOULUTUOUNUTULBUMEZUOUNOAUMBPUOVDUNUMBTQRUAVAUOOULUTUOUBUCUDUEVBUQU
      FUGASEUSVCUHCDASNUIUJUK $.
      $( [23-Jun-2013] $)
  $}

  ${
    $( The transitive closure function is idempotent.  (Contributed by Mario
       Carneiro, 23-Jun-2013.) $)
    tcidm $p |- ( TC ` ( TC ` A ) ) = ( TC ` A ) $=
      ( ctc cfv wss wtr ssid tctr cvv wcel wa fvex tcmin ax-mp mp2an tcid eqssi
      wi ) ABCZBCZRRRDZREZSRDZRFAGRHIZTUAJUBQABKZRRHLMNUCRSDUDRHOMP $.
      $( [23-Jun-2013] $)
  $}


