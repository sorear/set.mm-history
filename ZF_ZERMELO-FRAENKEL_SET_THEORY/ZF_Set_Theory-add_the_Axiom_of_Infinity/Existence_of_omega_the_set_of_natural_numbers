$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Existence of omega (the set of natural numbers)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d x y $.
    $( The existence of omega (the class of natural numbers).  Axiom 7 of
       [TakeutiZaring] p. 43.  This theorem is proved assuming the Axiom of
       Infinity and in fact is equivalent to it, as shown by the reverse
       derivation ~ inf0 .

       A finitist (someone who doesn't believe in infinity) could, without
       contradiction, replace the Axiom of Infinity by its denial
       ` -. om e. _V ` ; this would lead to ` om = _V ` (the universe of all
       sets) by ~ fineqv .  The finitist could still develop natural number,
       integer, and rational number arithmetic but would be denied the real
       numbers (as well as much of the rest of mathematics).  In deference to
       the finitist, much of our development is done, when possible, without
       invoking the Axiom of Infinity; an example is Peano's axioms ~ peano1
       through ~ peano5 (which many textbooks prove more easily assuming
       Infinity). $)
    omex $p |- om e. _V $=
      ( vx vy com cv wss wex wcel c0 csuc wral wa zfinf2 wi ax-1 ralimi2 peano5
      cvv sylan2 eximi ax-mp vex ssex exlimiv ) CADZEZAFZCQGZHUDGZBDZIUDGZBUDJZ
      KZAFUFABLULUEAUKUHUIUDGUJMZBCJUEUJUMBUDCUMUICGNOBUDPRSTUEUGACUDAUAUBUCT
      $.
      $( [6-Aug-1994] $)
  $}

  ${
    $d x y z w $.
    $( The first version of the Axiom of Infinity ~ ax-inf proved from the
       second version ~ ax-inf2 .  Note that we didn't use ~ ax-reg , unlike
       the other direction ~ axinf2 . $)
    axinf $p |- E. y ( x e. y /\
                 A. z ( z e. y -> E. w ( z e. w /\ w e. y ) ) ) $=
      ( omex inf0 ) ABCDEF $.
      $( [24-Apr-2009] $)
  $}

  ${
    $d x y z w $.
    $( The statement "there exists a set that is a proper subset of its union"
       is equivalent to the Axiom of Infinity (see theorem ~ infeq5 ).  This
       provides us with a very compact way to express of the Axiom of Infinity
       using only elementary symbols. $)
    inf5 $p |- E. x x C. U. x $=
      ( cv cuni wpss wex com cvv wcel omex infeq5 mpbir ) ABZLCDAEFGHIAJK $.
      $( [3-Jun-2005] $)
  $}

  $( Omega is an ordinal number.  (Revised by Mario Carneiro, 30-Jan-2013.) $)
  omelon $p |- om e. On $=
    ( com cvv wcel con0 omex omelon2 ax-mp ) ABCADCEFG $.
    $( [30-Jan-2013] $) $( [10-May-1998] $)

  ${
    $d x y z $.
    $( The class of natural numbers omega can be defined as the smallest
       "inductive set," which is valid provided we assume the Axiom of
       Infinity.  Definition 6.3 of [Eisenberg] p. 82. $)
    dfom3 $p |- om = |^| { x | ( (/) e. x /\ A. y e. x suc y e. x ) } $=
      ( vz com c0 cv wcel csuc wral wa cab cint wss 0ex elintab wceq rgen eleq2
      wi wal simpl mpgbir suceq eleq1d rcla4cv adantl a2i alimi vex 3imtr4i a1i
      sucex peano5 mp2an peano1 peano2 omex raleqbi1dv cla4v sylbi mp2ani ssriv
      anbi12d imbi12d eqssi ) DEAFZGZBFZHZVFGZBVFIZJZAKLZEVMGZCFZVMGZVOHZVMGZSZ
      CDIDVMMVNVLVGSAVLAENOVGVKUAUBVSCDVSVODGZVLVOVFGZSZATZVLVQVFGZSZATVPVRWBWE
      AVLWAWDVKWAWDSVGVJWDBVOVFVHVOPVIVQVFVHVOUCUDUEUFUGUHVLAVOCUIZOZVLAVQVOWFU
      LOUJUKQCVMUMUNCVMDVPEDGZVIDGZBDIZVTUOWIBDVHUPQVPWCWHWJJZVTSZWGWBWLADUQVFD
      PZVLWKWAVTWMVGWHVKWJVFDERVJWIBVFDVFDVIRURVCVFDVORVDUSUTVAVBVE $.
      $( [27-Jun-2004] $) $( [6-Aug-1994] $)
  $}

  ${
    $d A x $.
    $( A simplification of ~ elom assuming the Axiom of Infinity. $)
    elom3 $p |- ( A e. om <-> A. x ( Lim x -> A e. x ) ) $=
      ( com wcel cv wlim wi wal word wa elomg ibi simprd limom omex limeq eleq2
      wceq imbi12d cla4v mpi impbii ) BCDZAEZFZBUDDZGZAHZUCBIZUHUCUIUHJABCKLMUH
      CFZUCNUGUJUCGACOUDCRUEUJUFUCUDCPUDCBQSTUAUB $.
      $( [30-May-2003] $)
  $}

  ${
    $d x y $.
    $( A simplification of ~ df-om assuming the Axiom of Infinity. $)
    dfom4 $p |- om = { x | A. y ( Lim y -> x e. y ) } $=
      ( cv wlim wcel wi wal com elom3 abbi2i ) BCZDACZKEFBGAHBLIJ $.
      $( [30-May-2003] $)
  $}

  ${
    $d x y $.
    $( ` om ` is the smallest limit ordinal and can be defined as such
       (although the Axiom of Infinity is needed to ensure that at least one
       limit ordinal exists).  (Contributed by Mario Carneiro, 2-Feb-2013.) $)
    dfom5 $p |- om = |^| { x | Lim x } $=
      ( vy com cv wlim cab cint wcel wel wi wal elom3 vex elintab bitr4i eqriv
      ) BCADEZAFGZBDZCHQBAIJAKSRHASLQASBMNOP $.
      $( [2-Feb-2013] $)
  $}

  $( Ordinal addition is not commutative.  This theorem shows a
     counterexample.  Remark in [TakeutiZaring] p. 60. $)
  oancom $p |- ( 1o +o om ) =/= ( om +o 1o ) $=
    ( c1o com coa wcel wne con0 wceq omelon 1onn oaabslem mp2an csuc omex sucid
    co oa1suc ax-mp eleqtrri 1on oacl eqeltri wss wa wb onelpss simprbi ) ABCOZ
    BACOZDZUGUHEZUGBUHBFDZABDUGBGHIAJKBBLZUHBMNUKUHULGHBPQRUAUIUGUHUBZUJUGFDZUH
    FDZUIUMUJUCUDAFDZUKUNSHABTKUKUPUOHSBATKUGUHUEKUFQ $.
    $( [10-Dec-2004] $)

  $( A set is finite iff it is strictly dominated by the class of natural
     number.  Theorem 42 of [Suppes] p. 151.  The Axiom of Infinity is used for
     the forward implication.  (Contributed by FL, 16-Apr-2011.) $)
  isfinite3 $p |- ( A e. Fin <-> A ~< om ) $=
    ( cfn wcel com csdm wbr cdom wn wa isfinite1 omex ensym con3i anim2i brsdom
    cen sylibr syl isfinite2 impbii ) ABCZADEFZUAADGFZDAPFZHZIZUBAJUFUCADPFZHZI
    UBUEUHUCUGUDADKLMNADOQRAST $.
    $( [16-Apr-2011] $)

  $( A natural number is strictly dominated by the set of natural numbers. $)
  nnsdom $p |- ( A e. om -> A ~< om ) $=
    ( com wcel cdom wbr cen wn csdm omsdomnn omex ensym con3i anim2i syl brsdom
    wa sylibr ) ABCZABDEZABFEZGZPZABHERSBAFEZGZPUBAIUDUASTUCABJKLMNABOQ $.
    $( [28-Oct-2003] $)

  ${
    $d x y $.
    $( Omega is equinumerous to a proper subset of itself.  Example 13.2(4) of
       [Eisenberg] p. 216. $)
    omenps $p |- om ~~ ( om \ { (/) } ) $=
      ( com cvv wcel c0 csn cdif cen wbr omex limom limenpsi ax-mp ) ABCAADEFGH
      IABJKL $.
      $( [30-Jul-2003] $)
  $}

  $( The set of natural numbers is equinumerous to its successor. $)
  omensuc $p |- om ~~ suc om $=
    ( com cvv wcel csuc cen wbr omex limom limensuci ax-mp ) ABCAADEFGABHIJ $.
    $( [30-Oct-2003] $)

  ${
    $d x y z A $.
    $( Any unbounded subset of natural numbers is equinumerous to the set of
       all natural numbers.  This version of ~ unbnn eliminates its hypothesis
       by assuming the Axiom of Infinity. $)
    unbnn3 $p |- ( ( A C_ om /\ A. x e. om E. y e. A x e. y ) -> A ~~ om ) $=
      ( vz com wss cv wcel wrex wral cen wbr cvv wi omex ssex wceq sseq1 rexeq
      wa ralbidv anbi12d breq1 imbi12d vex unbnn vtoclg exp3a mpcom imp ) CEFZA
      GBGHZBCIZAEJZCEKLZCMHZUKUNUONCEOPUPUKUNUODGZEFZULBUQIZAEJZTZUQEKLZNUKUNTZ
      UONDCMUQCQZVAVCVBUOVDURUKUTUNUQCERVDUSUMAEULBUQCSUAUBUQCEKUCUDABUQDUEUFUG
      UHUIUJ $.
      $( [4-May-2005] $)
  $}

  ${
    $d w x y z F $.
    $( Using the Axiom of Regularity in the form ~ zfregfr , show that there
       are no infinite descending ` e. ` -chains.  Proposition 7.34 of
       [TakeutiZaring] p. 44.  (Revised by Mario Carneiro, 22-Mar-2013.) $)
    noinfep $p |- E. x e. om ( F ` suc x ) e/ ( F ` x ) $=
      ( vz vy vw cv cep wn com cfv wral wrex wnel cvv wcel wne ax-mp wceq fvex
      c0 wbr cmpt crn csuc wfr wss omex mptexg rnex zfregfr ssid cdm dmmptg a1i
      mprg peano1 ne0i eqnetri dm0rn0 necon3bii mpbi fri mp4an wfn wb rgen eqid
      mptfng fvelrnb peano2 fveq2 fvmpt syl fnfvelrn sylancr eqeltrrd wel eleq1
      epel syl5bb notbid df-nel syl6bbr rcla4cv syl5com syl5ibcom neleq2 biimpd
      wi eqeq1 syl6 com23 syld com12 reximdvai syl5bi rexlimiv ) CFZDFZGUAZHZCE
      IEFZBJZUBZUCZKZDXELZAFZUDZBJZXHBJZMZAILZXENOXEGUEXEXEUFXETPZXGXDINOXDNOUG
      EIXCNUHQUIXEUJXEUKXDULZTPXNXOITXCNOZXOIREIEIXCNUMXPXBIOXBBSUNZUOTIOITPUPI
      TUQQURXOTXETXDUSUTVADCXEXENGVBVCXFXMDXEXFWSXEOZXMXRXHXDJZWSRZAILZXFXMXDIV
      DZXRYAVEXPEIKYBXPEIXQVFEIXCXDXDVGZVHVAZAIWSXDVIQXFXTXLAIXHIOZXFXTXLWIZYEX
      FXJWSMZYFYEXJXEOXFYGYEXIXDJZXJXEYEXIIOZYHXJRXHVJZEXIXCXJIXDXBXIBVKYCXIBSV
      LVMYEYBYIYHXEOYDYJIXIXDVNVOVPXAYGCXJXEWRXJRZXAXJWSOZHYGYKWTYLWTCDVQYKYLCD
      VSWRXJWSVRVTWAXJWSWBWCWDWEYEXTYGXLYEXTWSXKRZYGXLWIYEXSXKRXTYMEXHXCXKIXDXB
      XHBVKYCXHBSVLXSWSXKWJWFYMYGXLWSXKXJWGWHWKWLWMWNWOWPWNWQQ $.
      $( [22-Mar-2013] $) $( [26-Jan-2006] $)
  $}

  ${
    $d x y z F $.
    $( Using the Axiom of Regularity in the form ~ zfregfr , show that there
       are no infinite descending ` e. ` -chains.  Proposition 7.34 of
       [TakeutiZaring] p. 44. $)
    noinfepOLD $p |- ( F Fn om -> E. x e. om -. ( F ` suc x ) e. ( F ` x ) ) $=
      ( vz vy com wfn cv cep wn wrex cfv wcel cvv c0 wne wceq eleq2 syl notbid
      wa wbr crn wral csuc cdm wfun fndm omex syl6eqel fnfun sylc peano1 mpbiri
      funrnex ne0i dm0rn0 necon3bii sylib wfr zfregfr wss mpanr1 mpanl2 syl2anc
      ssid fri wi fvelrnb adantr peano2 fnfvelrn adantlr simplr jca epel syl5bb
      wb eleq1 rcla4va syl5ibr sylan2i com12 reximdva sylbid com23 rexlimdv mpd
      syl5 ex ) BEFZCGZDGZHUAZIZCBUBZUCZDWOJZAGZUDZBKZWRBKZLZIZAEJZWJWOMLZWONOZ
      WQWJBUEZMLBUFXEWJXGEMEBUGZUHUIEBUJMBUNUKWJXGEPZXFXHXIXGNOZXFXINXGLZXJXIXK
      NELULXGENQUMXGNUORXGNWONBUPUQURRXEWOHUSZXFWQWOUTXEXLTWOWOVAXFWQWOVEDCWOWO
      MHVFVBVCVDWJWPXDDWOWJWPWLWOLZXDWJWPXMXDVGWJWPTZXMXAWLPZAEJZXDWJXMXPVQWPAE
      WLBVHVIXNXOXCAEXOXNWRELZTXCXQXOXNWSELZXCWRVJXNXRTZWTWOLZWPTZXOXCXSXTWPWJX
      RXTWPEWSBVKVLWJWPXRVMVNYAXCXOWTWLLZIZWNYCCWTWOWKWTPZWMYBWMWKWLLYDYBCDVOWK
      WTWLVRVPSVSXOXBYBXAWLWTQSVTWHWAWBWCWDWIWEWFWG $.
      $( [26-Jan-2006] $)
  $}


