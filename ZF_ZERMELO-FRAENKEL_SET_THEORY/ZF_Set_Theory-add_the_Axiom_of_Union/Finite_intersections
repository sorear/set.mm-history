$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Finite intersections
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c fi $.
  $( Extend class notation with the function whose value is the class of all
     the finite intersections of the elements of a given set. $)
  cfi $a class fi $.

  ${
    $d x y z $.
    $( Function whose value is the class of all the finite intersections of the
       elements of ` x ` .  (Contributed by FL, 2-Sep-2008.) $)
    df-fi $a |- fi = ( x e. _V |-> { z |
                  E. y e. ( ~P x i^i Fin ) z = |^| y } ) $.
  $}

  ${
    $d A x y z $.  $d B x y $.
    $( The set of all the finite intersections of the elements of ` A ` .
       (Contributed by FL, 2-Sep-2008.) $)
    fival $p |- ( A e. V -> ( fi ` A ) = { y |
                  E. x e. ( ~P A i^i Fin ) y = |^| x } ) $=
      ( vz wcel cvv cv cint wceq cpw cfn cin wrex cab cfi cfv wss syl sylibr wa
      elex cuni simpr c0 wne inss1 sseli elpwi vex eleq1 mpbii intssuni2 syl2an
      intex eqsstrd elpw rexlimiva abssi uniexg pwexg ssexg sylancr pweq ineq1d
      rexeqdv abbidv df-fi fvmptg syl2anc ) CDFZCGFBHZAHZIZJZACKZLMZNZBOZGFZCPQ
      VSJCDUBVKVSCUCZKZRWBGFZVTVRBWBVOVLWBFZAVQVMVQFZVOUAZVLWARWDWFVLVNWAWEVOUD
      WEVMCRZVMUEUFZVNWARVOWEVMVPFWGVQVPVMVPLUGUHVMCUISVOVNGFZWHVOVLGFWIBUJZVLV
      NGUKULVMUOTVMCUMUNUPVLWAWJUQTURUSVKWAGFWCCDUTWAGVASVSWBGVBVCECVOAEHZKZLMZ
      NZBOVSGGPWKCJZWNVRBWOVOAWMVQWOWLVPLWKCVDVEVFVGEABVHVIVJ $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( Specific properties of an element of ` ( fi `` B ) ` .  (Contributed by
       FL, 2-Sep-2008.) $)
    elfi $p |- ( ( A e. V /\ B e. W ) -> ( A e. ( fi ` B ) <->
        E. x e. ( ~P B i^i Fin ) A = |^| x ) ) $=
      ( vy wcel cfi cfv cv cint wceq cpw cfn cin wrex cab fival eleq2d eqeq1
      rexbidv elabg sylan9bbr ) CEGZBCHIZGBFJZAJKZLZACMNOZPZFQZGBDGBUGLZAUIPZUD
      UEUKBAFCERSUJUMFBDUFBLUHULAUIUFBUGTUAUBUC $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( Sufficient condition for an element of ` ( fi `` B ) ` .  (Contributed
       by Mario Carneiro, 24-Nov-2013.) $)
    elfir $p |- ( ( B e. V /\ ( A C_ B /\ A =/= (/) /\ A e. Fin ) ) ->
        |^| A e. ( fi ` B ) ) $=
      ( vx wcel wss c0 wne cfn w3a wa cint cfi cfv cv wceq cpw cin wrex cvv imp
      simp1 elpw2g syl5ibr simpr3 elin sylanbrc inteq eqeq2d rcla4ev sylancl wb
      eqid simp2 intex sylib id elfi syl2anr mpbird ) BCEZABFZAGHZAIEZJZKZALZBM
      NEZVGDOZLZPZDBQZIRZSZVFAVMEZVGVGPZVNVFAVLEZVDVOVAVEVQVEVQVAVBVBVCVDUBABCU
      CUDUAVAVBVCVDUEAVLIUFUGVGUMVKVPDAVMVIAPVJVGVGVIAUHUIUJUKVEVGTEZVAVHVNULVA
      VEVCVRVBVCVDUNAUOUPVAUQDVGBTCURUSUT $.
      $( [24-Nov-2013] $)
  $}

  ${
    $d A x y $.  $d x V $.
    $( Any element of a set ` A ` is the intersection of a finite subset of
       ` A ` .  (Contributed by FL, 27-Apr-2008.) $)
    ssfii $p |- ( A e. V -> A C_ ( fi ` A ) ) $=
      ( vx vy wcel cfi cfv cint wceq cpw cfn cin wrex csn snelpwi snfi a1i elin
      cv cvv sylanbrc intsn eqcomi inteq eqeq2d rcla4ev sylancl wb elfi syl5ibr
      vex mpan ssrdv ) ABEZCAAFGZCSZAEZUPUOEZUNUPDSZHZIZDAJZKLZMZUQUPNZVCEZUPVE
      HZIZVDUQVEVBEVEKEZVFUPAOVIUQUPPQVEVBKRUAVGUPUPCUKZUBUCVAVHDVEVCUSVEIUTVGU
      PUSVEUDUEUFUGUPTEUNURVDUHVJDUPATBUIULUJUM $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)

    $( If ` A ` is not empty, the class of all the finite intersections of
       ` A ` is not empty either.  (Contributed by FL, 2-Sep-2008.) $)
    fi0 $p |- ( A e. V -> ( A = (/) <-> ( fi ` A ) = (/) ) ) $=
      ( vy vx wcel c0 wceq cfi cfv fveq2 cv cint cpw cfn wrex cvv wn necon1bbii
      wex wss cin cab 0ex fival ax-mp inss1 sseli elpwi 3syl intex isset sylibr
      ss0 wne bitri nrex rexcom4 mtbi abn0 mpbi eqtri ssfii sseq0 sylan impbid2
      syl6eq ex ) ABEZAFGZAHIZFGZVIVJFHIZFAFHJVLCKDKZLZGZDFMZNUAZOZCUBZFFPEVLVS
      GUCDCFPUDUEVRCSZQVSFGVOCSZDVQOVTWADVQVMVQEZVMFGZWAQWBVMVPEVMFTWCVQVPVMVPN
      UFUGVMFUHVMUMUIWAVMFVMFUNVNPEWAVMUJCVNUKUORULUPVODCVQUQURVTVSFVRCUSRUTVAV
      FVHVKVIVHAVJTVKVIABVBAVJVCVDVGVE $.
      $( [24-Nov-2013] $) $( [27-Apr-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( The elements of ` ( fi `` C ) ` are closed under finite intersection. $)
    fiin $p |- ( ( A e. ( fi ` C ) /\ B e. ( fi ` C ) ) ->
                 ( A i^i B ) e. ( fi ` C ) ) $=
      ( vz vx vy cfi wcel wa cin cv cint wceq cfn wrex cvv wb elfi wss elin cfv
      cpw wne ne0i fvprc necon1ai syl mpdan ibi adantr simpr ancoms sylan mpbid
      c0 wi cun elpwi anim12i unss sylib vex unex elpw sylibr unfi an4s anbi12i
      3imtr4i ineq12 intun syl6eqr inteq eqeq2d syl2an expr rexlimdva rexlimiva
      rcla4ev sylc inex1g syl2anc mpbird ) ACGUAZHZBWDHZIZABJZWDHZWHDKZLZMZDCUB
      ZNJZOZWGAEKZLZMZEWNOZBFKZLZMZFWNOZWOWEWSWFWEWSWECPHZWEWSQWEWDUOUCXDWDAUDX
      DWDUOCGUEUFUGZEACWDPRUHUIUJWGWFXCWEWFUKWEXDWFWFXCQZXEWFXDXFFBCWDPRULUMUNW
      RXCWOUPEWNWPWNHZWRIZXBWOFWNXHWTWNHZXBWOXGXIWRXBWOXGXIIZWPWTUQZWNHZWHXKLZM
      ZWOWRXBIZWPWMHZWPNHZIZWTWMHZWTNHZIZIXKWMHZXKNHZIZXJXLXPXSXQXTYDXPXSIZYBXQ
      XTIYCYEXKCSZYBYEWPCSZWTCSZIYFXPYGXSYHWPCURWTCURUSWPWTCUTVAXKCWPWTEVBFVBVC
      VDVEWPWTVFUSVGXGXRXIYAWPWMNTWTWMNTVHXKWMNTVIXOWHWQXAJXMAWQBXAVJWPWTVKVLWL
      XNDXKWNWJXKMWKXMWHWJXKVMVNVSVOVGVPVQVRVTWEWIWOQZWFWEWHPHXDYIABWDWAXEDWHCP
      PRWBUJWC $.
      $( [24-Nov-2013] $)
  $}

  ${
    $d t x y z A $.
    $( The set of finite intersections is the smallest set that contains ` A `
       and is closed under pairwise intersection. $)
    dffi2 $p |- ( A e. V -> ( fi ` A ) =
        |^| { z | ( A C_ z /\ A. x e. z A. y e. z ( x i^i y ) e. z ) } ) $=
      ( vt wcel cvv cfi cv wss cin wral wa cint wceq wi wal cfn syl cfv cab cpw
      elex wrex wb vex elfi mpan biimpd wex df-rex c0 wne w3a fiint inss1 sseli
      elpwi 3ad2ant2 simp1 sstrd eleq1 mpbii intex sylibr 3ad2ant3 inss2 3expib
      3jca pm2.27 biimprd adantl a1i syldd com23 alimdv syl5bi imp 19.23v sylib
      syl6 sylan9 ssrdv alrimiv ssintab ssfii fiin rgen2a fvex sseq2 raleqbi1dv
      ex eleq2 anbi12d elab sylanbrc intss1 eqssd ) DEGDHGZDIUAZDCJZKZAJZBJZLZX
      BGZBXBMZAXBMZNZCUBZOZPDEUDWTXAXLWTXJXAXBKZQZCRXAXLKWTXNCWTXJXMWTXJNFXAXBW
      TFJZXAGZXOXDOZPZADUCZSLZUEZXJXOXBGZWTXPYAXOHGZWTXPYAUFFUGZAXODHHUHUIUJYAX
      DXTGZXRNZAUKZXJYBXRAXTULXJYFYBQZARZYGYBQXCXIYIXIXDXBKZXDUMUNZXDSGZUOZXQXB
      GZQZARXCYIABXBUPXCYOYHAXCYFYOYBXCYFYOYNYBXCYFYMYOYNQXCYEXRYMXCYEXRUOZYJYK
      YLYPXDDXBYEXCXDDKZXRYEXDXSGYQXTXSXDXSSUQURXDDUSTUTXCYEXRVAVBXRXCYKYEXRXQH
      GZYKXRYCYRYDXOXQHVCVDXDVEVFVGYEXCYLXRXTSXDXSSVHURUTVJVIYMYNVKWBYFYNYBQZQX
      CXRYSYEXRYBYNXOXQXBVCVLVMVNVOVPVQVRVSYFYBAVTWAVRWCWDWMWEXJCXAWFVFWTXAXKGZ
      XLXAKWTDXAKZXFXAGZBXAMZAXAMZYTDHWGUUDWTUUBABXAXDXEDWHWIVNXJUUAUUDNCXADIWJ
      XBXAPXCUUAXIUUDXBXADWKXHUUCAXBXAXGUUBBXBXAXBXAXFWNWLWLWOWPWQXAXKWRTWST $.
      $( [24-Nov-2013] $)

    $( The set of finite intersections of a set is contained in the powerset of
       the union of the elements of ` A ` . $)
    fipwuni $p |- ( A e. V -> ( fi ` A ) C_ ~P U. A ) $=
      ( vz vx vy wcel cfi cfv cv wss cin wral cab cint cuni cpw elpw raleqbi1dv
      wa cvv dffi2 pwuni ssinss1 vex inex1 adantr rgen2a pm3.2i wb uniexg pwexg
      3imtr4i wceq sseq2 eleq2 anbi12d elabg 3syl mpbiri intss1 syl eqsstrd ) A
      BFZAGHACIZJZDIZEIZKZVDFZEVDLZDVDLZSZCMZNZAOZPZDECABUAVCVPVMFZVNVPJVCVQAVP
      JZVHVPFZEVPLZDVPLZSZVRWAAUBVSDEVPVFVPFZVSVGVPFVFVOJVHVOJWCVSVFVGVOUCVFVOD
      UDZQVHVOVFVGWDUEQULUFUGUHVCVOTFVPTFVQWBUIABUJVOTUKVLWBCVPTVDVPUMVEVRVKWAV
      DVPAUNVJVTDVDVPVIVSEVDVPVDVPVHUORRUPUQURUSVPVMUTVAVB $.
      $( [24-Nov-2013] $)

    $( A set which is closed under pairwise intersection is closed under finite
       intersection.  (Contributed by Jeff Madsen, 2-Sep-2009.) $)
    inficl $p |- ( A e. V ->
        ( A. x e. A A. y e. A ( x i^i y ) e. A <-> ( fi ` A ) = A ) ) $=
      ( vz wcel cv cin wral cfi cfv wceq wss ssfii wa cab cint eleq2 raleqbi1dv
      eqimss2 bitr3d elabg intss1 syl6bir dffi2 sseq1d sylibrd eqss simplbi2com
      biantrurd sylsyld fiin rgen2a mpbii impbid1 ) CDFZAGZBGZHZCFZBCIZACIZCJKZ
      CLZUPCVCMZVBVCCMZVDCDNUPVBCEGZMZUSVGFZBVGIZAVGIZOZEPZQZCMZVFUPVBCVMFVOVLV
      BECDVGCLZVKVLVBVPVHVKCVGTUJVJVAAVGCVIUTBVGCVGCUSRSSUAUBCVMUCUDUPVCVNCABEC
      DUEUFUGVDVFVEVCCUHUIUKVDUSVCFZBVCIZAVCIVBVQABVCUQURCULUMVRVAAVCCVQUTBVCCV
      CCUSRSSUNUO $.
      $( [24-Nov-2013] $) $( [2-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d y V $.
    $( Subset relationship for function ` fi ` .  (Contributed by Jeff Hankins,
       7-Oct-2009.) $)
    fiss $p |- ( ( B e. V /\ A C_ B ) -> ( fi ` A ) C_ ( fi ` B ) ) $=
      ( vy vx vz wcel wss wa cv cin wral cab cint cfi cfv syl cvv wceq dffi2 wi
      sstr2 adantl anim1d ss2abdv intss ssexg ancoms adantr 3sstr4d ) BCGZABHZI
      ZADJZHZEJFJKUNGFUNLEUNLZIZDMZNZBUNHZUPIZDMZNZAOPZBOPZUMVBURHUSVCHUMVAUQDU
      MUTUOUPULUTUOUAUKABUNUBUCUDUEVBURUFQUMARGZVDUSSULUKVFABCUGUHEFDARTQUKVEVC
      SULEFDBCTUIUJ $.
      $( [24-Nov-2013] $) $( [7-Oct-2009] $)
  $}

  $( The union of the finite intersections of a set is simply the union of the
     set itself.  (Contributed by Jeff Hankins, 5-Sep-2009.) $)
  fiuni $p |- ( A e. V -> U. A = U. ( fi ` A ) ) $=
    ( wcel cuni cfi cfv wss ssfii uniss syl cpw fipwuni unipw syl6sseq eqssd )
    ABCZADZAEFZDZPARGQSGABHARIJPSQKZDZQPRTGSUAGABLRTIJQMNO $.
    $( [24-Nov-2013] $) $( [5-Sep-2009] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d x y z K $.
    $( A finite intersection of elements taken from a union of collections.
       (Contributed by Jeff Hankins, 15-Nov-2009.)  (The proof was shortened by
       Mario Carneiro, 26-Nov-2013.) $)
    elfiun $p |- ( ( B e. D /\ C e. K ) -> ( A e. ( fi ` ( B u. C ) ) <-> ( A
    e. ( fi ` B ) \/ A e. ( fi ` C ) \/ E. x e. ( fi ` B ) E. y e. ( fi ` C ) A
    = ( x i^i y ) ) ) ) $=
      ( wcel wa cin wceq wrex cvv wi 3expib cfn c0 wss cdif syl cun cfi cfv w3o
      vz cv elex adantl simpll simplr 3jca 3anim1i inex1 eleq1 mpbiri rexlimivv
      w3a vex a1i 3jaoi impcom cint cpw wb simp1 unexg 3adant1 elfi syl2anc wne
      simpl1 intex syl6bbr syl5ibcom wn simp23 simp3l inss1 sseli elpwi reldisj
      nne 3syl mpbid uncom difeq1i difun2 eqtri difss syl6ss simp3r inss2 elfir
      eqsstri syl13anc 3mix2 sylbi simp22 3mix1 simp1l ssfi mpan2 simp1r biimpi
      indi df-ss syl5reqr inteqd intun syl6eq ineq1 eqeq2d ineq2 rcla42ev 3mix3
      syl3anc ecase eqeq1 3orbi123d syl5ibrcom expr com23 mpdd rexlimdva sylbid
      2rexbidv ssun1 fiss sylancl sseld ssun2 anim12d fiin syl6 rexlimdvv 3jaod
      eleq1a impbid pm5.21nd ) DFHZEGHZIZCDEUAZUBUCZHZCDUBUCZHZCEUBUCZHZCAUFZBU
      FZJZKZBUUHLAUUFLZUDZCMHZYTUUAUQZUUBUUEIUUPYTUUAUUEUUPUUBCUUDUGUHYTUUAUUEU
      IYTUUAUUEUJUKUUOUUBUUQUUGUUBUUQNUUIUUNUUGYTUUAUUQUUGUUPYTUUACUUFUGULOUUIY
      TUUAUUQUUIUUPYTUUACUUHUGULOUUNYTUUAUUQUUNUUPYTUUAUUMUUPABUUFUUHUUMUUPNUUJ
      UUFHZUUKUUHHZIZUUMUUPUULMHUUJUUKAURUMCUULMUNUOUSUPULOUTVAUUQUUEUUOUUQUUEC
      UEUFZVBZKZUEUUCVCZPJZLZUUOUUQUUPUUCMHZUUEUVFVDUUPYTUUAVEYTUUAUVGUUPDEFGVF
      ZVGUECUUCMMVHVIUUQUVCUUOUEUVEUUQUVAUVEHZIZUVCUVAQVJZUUOUVJUUPUVCUVKUUPYTU
      UAUVIVKUVCUUPUVBMHUVKCUVBMUNUVAVLVMVNUVJUVKUVCUUOUUQUVIUVKUVCUUONUUQUVIUV
      KIZIZUUOUVCUVBUUFHZUVBUUHHZUVBUULKZBUUHLAUUFLZUDZUVADJZQVJZUVAEJZQVJZUVMU
      VRNZUVTVOUVSQKZUWCUVSQWBUWDUUQUVLUVRUWDUUQUVLUQZUVOUVRUWEUUAUVAERUVKUVAPH
      ZUVOUWDUUPYTUUAUVLVPUWEUVAUUCDSZEUWEUWDUVAUWGRZUWDUUQUVLVEUWEUVIUVAUUCRZU
      WDUWHVDUWDUUQUVIUVKVQZUVIUVAUVDHUWIUVEUVDUVAUVDPVRVSUVAUUCVTTZUVADUUCWAWC
      WDUWGEDSZEUWGEDUAZDSUWLUUCUWMDDEWEWFEDWGWHEDWIWNWJUWDUUQUVIUVKWKUWEUVIUWF
      UWJUVEPUVAUVDPWLVSZTUVAEGWMWOUVOUVNUVQWPTOWQUWBVOUWAQKZUWCUWAQWBUWOUUQUVL
      UVRUWOUUQUVLUQZUVNUVRUWPYTUVADRUVKUWFUVNUWOUUPYTUUAUVLWRUWPUVAUUCESZDUWPU
      WOUVAUWQRZUWOUUQUVLVEUWPUVIUWIUWOUWRVDUWOUUQUVIUVKVQZUWKUVAEUUCWAWCWDUWQD
      ESDDEWGDEWIWNWJUWOUUQUVIUVKWKUWPUVIUWFUWSUWNTUVADFWMWOUVNUVOUVQWSTOWQUVTU
      WBIZUUQUVLUVRUWTUUQUVLUQZUVQUVRUXAUVSVBZUUFHZUWAVBZUUHHZUVBUXBUXDJZKZUVQU
      XAYTUVSDRZUVTUVSPHZUXCUWTUUPYTUUAUVLWRUXHUXAUVADWLUSUVTUWBUUQUVLWTUXAUVIU
      WFUXIUWTUUQUVIUVKVQZUWNUWFUVSUVARUXIUVADVRUVAUVSXAXBWCUVSDFWMWOUXAUUAUWAE
      RZUWBUWAPHZUXEUWTUUPYTUUAUVLVPUXKUXAUVAEWLUSUVTUWBUUQUVLXCUXAUVIUWFUXLUXJ
      UWNUWFUWAUVARUXLUVAEVRUVAUWAXAXBWCUWAEGWMWOUXAUVIUWIUXGUXJUWKUWIUVBUVSUWA
      UAZVBUXFUWIUVAUXMUWIUXMUVAUUCJZUVAUVADEXEUWIUXNUVAKUVAUUCXFXDXGXHUVSUWAXI
      XJWCUVPUXGUVBUXBUUKJZKABUXBUXDUUFUUHUUJUXBKUULUXOUVBUUJUXBUUKXKXLUUKUXDKU
      XOUXFUVBUUKUXDUXBXMXLXNXPUVQUVNUVOXOTOXQUVCUUGUVNUUIUVOUUNUVQCUVBUUFUNCUV
      BUUHUNUVCUUMUVPABUUFUUHCUVBUULXRYFXSXTYAYBYCYDYEUUQUUGUUEUUIUUNUUQUUFUUDC
      YTUUAUUFUUDRZUUPUUBUVGDUUCRUXPUVHDEYGDUUCMYHYIVGZYJUUQUUHUUDCYTUUAUUHUUDR
      ZUUPUUBUVGEUUCRUXRUVHEDYKEUUCMYHYIVGZYJUUQUUMUUEABUUFUUHUUQUUTUUJUUDHZUUK
      UUDHZIZUUMUUENZUUQUURUXTUUSUYAUUQUUFUUDUUJUXQYJUUQUUHUUDUUKUXSYJYLUYBUULU
      UDHUYCUUJUUKUUCYMUULUUDCYQTYNYOYPYRYS $.
      $( [26-Nov-2013] $) $( [15-Nov-2009] $)
  $}


