$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
      Undefined values and restricted iota (description binder)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c Undef $.
  $c iota_ $.

  $( Extend class notation with undefined value function. $)
  cund $a class Undef $.

  $( Extend class notation with restricted description binder. $)
  crio $a class ( iota_ x e. A ph ) $.

  $( Def. 8.3.36 p. 181. $)
  $( Define the undefined value function, whose value at set ` s ` is
     guaranteed not to be a member of ` s ` (see ~ pwuninel ). $)
  df-undef $a |- Undef = ( s e. _V |-> ~P U. s ) $.

  ${
    $d s S $.
    undefval.1 $e |- S e. _V $.
    $( Value of the undefined value function.  Normally we will not reference
       the explicit value but will use ~ undefnel instead. $)
    undefval $p |- ( Undef ` S ) = ~P U. S $=
      ( vs cvv wcel cund cfv cuni wceq cv unieq pweqd df-undef uniex pwex fvmpt
      cpw ax-mp ) ADEAFGAHZQZIBCACJZHZQTDFUAAIUBSUAAKLCMSABNOPR $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel2 $p |- -. ( Undef ` S ) e. S $=
      ( cund cfv wcel cuni cpw pwuninel undefval eleq1i mtbir ) ACDZAEAFGZAEAHL
      MAABIJK $.
      $( [15-Sep-2011] $)

    $( The undefined value generated from a set is not a member of the set. $)
    undefnel $p |- ( Undef ` S ) e/ S $=
      ( cund cfv wnel wcel wn undefnel2 df-nel mpbir ) ACDZAEKAFGABHKAIJ $.
      $( [15-Sep-2011] $)
  $}

  ${
    $( Define restricted description binder.  In case it doesn't exist, we
       return a set which is not a member of the domain of discourse ` A ` .
       See also comments for ~ df-iota . $)
    df-riota $a |- ( iota_ x e. A ph ) =
       if ( E! x e. A ph , ( iota x ( x e. A /\ ph ) ) , ( Undef ` A ) ) $.
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqdv.1 $e |- ( ph -> A = B ) $.
    $( Formula-building deduction rule for iota. $)
    riotaeqdv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ps ) ) $=
      ( wreu cv wcel wa cio cund cfv cif crio wceq wb reueq1 syl df-riota
      eleq2d anbi1d iotabidv fveq2d ifbieq12d 3eqtr4g ) ABCDGZCHZDIZBJZCKZDLMZN
      BCEGZUHEIZBJZCKZELMZNBCDOBCEOAUGUMUKULUPUQADEPUGUMQFBCDERSAUJUOCAUIUNBADE
      UHFUAUBUCADELFUDUEBCDTBCETUF $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x ph $.
    riotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for restricted iota. $)
    riotabidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu cv wcel wa cio cund cfv cif reubidv anbi2d iotabidv eqidd df-riota
      crio ifbieq12d 3eqtr4g ) ABDEGZDHEIZBJZDKZELMZNCDEGZUDCJZDKZUGNBDETCDETAU
      CUHUFUGUJUGABCDEFOAUEUIDABCUDFPQAUGRUABDESCDESUB $.
      $( [15-Sep-2011] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ph $.
    riotaeqbidv.1 $e |- ( ph -> A = B ) $.
    riotaeqbidv.2 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equality deduction for restricted universal quantifier. $)
    riotaeqbidv $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. B ch ) ) $=
      ( crio riotabidv riotaeqdv eqtrd ) ABDEICDEICDFIABCDEHJACDEFGKL $.
      $( [15-Sep-2011] $)
  $}

  $( Restricted iota is a set. $)
  riotaex $p |- ( iota_ x e. A ps ) e. _V $=
    ( crio wreu cv wcel cio cund cfv cif cvv df-riota iotaex fvex ifex eqeltri
    wa ) ABCDABCEZBFCGARZBHZCIJZKLABCMSUAUBTBNCIOPQ $.
    $( [15-Sep-2011] $)

  $( An iota restricted to the universe is unrestricted. $)
  riotav $p |- ( iota_ x e. _V ph ) = ( iota x ph ) $=
    ( cvv crio wreu cv wcel wa cio cund cfv cif df-riota wceq iftrue iotabi vex
    wb wn c0 biantrur mpg syl6reqr reuv iotanul sylnbi vprc fvprc ax-mp syl6eqr
    weu iffalse eqtr4d pm2.61i eqtr4i ) ABCDABCEZBFCGZAHZBIZCJKZLZABIZABCMUPVBV
    ANUPVAUSVBUPUSUTOAURRVBUSNBAURBPUQABQUAUBUCUPSZVBUTVAVCVBTUTUPABUKVBTNABUDA
    BUEUFCCGSUTTNUGCJUHUIUJUPUSUTULUMUNUO $.
    $( [18-Sep-2011] $)

  $( Restricted iota in terms of iota. $)
  riotaiota $p |- ( E! x e. A ph
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( wreu crio cv wcel wa cio cund cfv cif df-riota iftrue syl5eq ) ABCDZABCEP
    BFCGAHBIZCJKZLQABCMPQRNO $.
    $( [15-Sep-2011] $)

  $( For proper classes, restricted and unrestricted iota are the same. $)
  riotaprc $p |- ( -. A e. _V
         -> ( iota_ x e. A ph ) = ( iota x ( x e. A /\ ph ) ) ) $=
    ( cvv wcel wn wreu crio cv wa cio wceq cund c0 fvprc adantr df-riota adantl
    cfv cif iffalse syl5eq weu df-reu iotanul sylnbi 3eqtr4d riotaiota pm2.61d2
    ex ) CDEFZABCGZABCHZBICEAJZBKZLZUKULFZUPUKUQJZCMSZNUMUOUKUSNLUQCMOPURUMULUO
    USTZUSABCQUQUTUSLUKULUOUSUARUBUQUONLZUKULUNBUCVAABCUDUNBUEUFRUGUJABCUHUI $.
    $( [15-Sep-2011] $)

  $( Restricted iota in terms of class union. $)
  riotauni $p |- ( E! x e. A ph
                    -> ( iota_ x e. A ph ) = U. { x e. A | ph } ) $=
    ( wreu crio cv wcel wa cio crab cuni riotaiota cab weu df-reu iotaequ sylbi
    wceq df-rab unieqi syl6eqr eqtrd ) ABCDZABCEBFCGAHZBIZABCJZKZABCLUCUEUDBMZK
    ZUGUCUDBNUEUIRABCOUDBPQUFUHABCSTUAUB $.
    $( [11-Oct-2011] $)

  ${
    $d x y A $.  $d y ph $.
    $( The abstraction variable in a restricted iota descriptor isn't free. $)
    hbriota1 $p |- ( y e. ( iota_ x e. A ph )
                       -> A. x y e. ( iota_ x e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hbreu1 hbiota1 hbfv hbif
      ax-17 hbxfreq ) BCABDEABDFZBGDHAIZBJZDKLZMABDNUABCUCUDABDOUBBCPBCDKCGZKHB
      SUEDHBSQRT $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.
    hbriota.1 $e |- ( ph -> A. x ph ) $.
    hbriota.2 $e |- ( z e. A -> A. x z e. A ) $.
    $( A variable not free in a wff remains so in a restricted iota
       descriptor. $)
    hbriota $p |- ( z e. ( iota_ y e. A ph )
                       -> A. x z e. ( iota_ y e. A ph ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif df-riota hblem hbreu hban hbxfreq
      hbiota ax-17 hbfv hbif ) BDACEHACEIZCJEKZALZCMZENOZPACEQUFBDUIUJABCEBDCEG
      RZFSUHBCDUGABUKFTUBBDENDJNKBUCGUDUEUA $.
      $( [12-Oct-2011] $)
  $}

  ${
    $d x y z $.  $d z A $.  $d z ph $.  $d z ps $.
    hbriotad.1 $e |- ( ph -> A. x ph ) $.
    hbriotad.2 $e |- ( ph -> A. y ph ) $.
    hbriotad.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    hbriotad.4 $e |- ( ph -> ( z e. A -> A. x z e. A ) ) $.
    $( Deduction version of ~ hbriota . $)
    hbriotad $p |- ( ph -> ( z e. ( iota_ y e. A ps )
                       -> A. x z e. ( iota_ y e. A ps ) ) ) $=
      ( crio wreu cv wcel wa cio cund cfv cif wceq df-riota ax-17 hblemd hbreud
      a1i hband hbiotad a17d hbfvd hbifd hbxfrd ) ACEBDFKZBDFLZDMFNZBOZDPZFQRZS
      ZGULURTABDFUAUEAUMCEUPUQGABCDFGHACEDFGAEUBJUCZIUDAUOCDEGHAUNBCUSIUFUGACEF
      QGAEMQNCUHJUIUJUK $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d x z A $.  $d y A $.  $d y z ph $.  $d x z ps $.
    cbvriota.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Change bound variable in a restricted description binder. $)
    cbvriota $p |- ( iota_ x e. A ph ) = ( iota_ y e. A ps ) $=
      ( vz wreu cv wcel wa cio cif crio wsbc ax-17 wb wal wceq ax-gen cund sban
      cfv cbvreuv sb8iota iotabi ax-mp clelsb3 anbi1i eleq1 sbie syl6bb anbi12d
      sbequ hbsb hban cbviotaf eqtri 3eqtri eqid ifbieq12i df-riota 3eqtr4i ) A
      CEHZCIEJZAKZCLZEUAUCZMBDEHZDIZEJZBKZDLZVHMACENBDENVDVIVGVHVMVHABCDEFUDVGV
      FCGIZOZGLZVECVNOZACVNOZKZGLZVMVFCGVFGPUEVOVSQZGRVPVTSWAGVEACGUBTVOVSGUFUG
      VTVNEJZVRKZGLZVMVSWCQZGRVTWDSWEGVQWBVRGCEUHUITVSWCGUFUGWCVLGDVNVJSZWBVKVR
      BVNVJEUJWFVRACVJOBAGDCUNABCDBCPFUKULUMWBVRDWBDPACGDADPUOUPVLGPUQURUSVHUTV
      AACEVBBDEVBVC $.
      $( [18-Mar-2013] $)
  $}

  ${
    $d y z A $.  $d x z w B $.  $d w z ph $.  $d w x y $.
    $( Interchange class substitution and restricted description binder. $)
    csbriotag $p |- ( A e. V
      -> [_ A / x ]_ ( iota_ y e. B ph ) = ( iota_ y e. B [ A / x ] ph ) ) $=
      ( vz vw crio csb wsbc wceq csbeq1 dfsbcq riotabidv eqeq12d vex hbs1 wcel
      cv ax-17 hbriota sbceq1a csbief vtoclg ) BGTZACEIZJZABUFKZCEIZLBDUGJZABDK
      ZCEIZLGDFUFDLZUHUKUJUMBUFDUGMUNUIULCEABUFDNOPBHUFUGUJGQUIBCHEABGRHTESBUAU
      BBTUFLAUICEABUFUCOUDUE $.
      $( [24-Feb-2013] $)
  $}

  ${
    $d x A $.
    $( Closure of restricted iota. $)
    riotacl $p |- ( E! x e. A ph -> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio cv wcel wa cio riotaiota reiotacl eqeltrd ) ABCDABCEBFCGAHBIC
      ABCJABCKL $.
      $( [16-Jan-2012] $)
  $}

  $( Restricted iota equals the undefined value of its domain of discourse
     ` A ` when not meaningful. $)
  riotaund $p |- ( -. E! x e. A ph
                    -> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
    ( wreu wn crio cv wcel wa cio cund cfv cif df-riota iffalse syl5eq ) ABCDZE
    ABCFQBGCHAIBJZCKLZMSABCNQRSOP $.
    $( [16-Jan-2012] $)

  ${
    $d x A $.
    riotaclb.1 $e |- A e. _V $.
    $( Closure of restricted iota. $)
    riotaclb $p |- ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) $=
      ( wreu crio wcel riotacl wn cund cfv undefnel2 cv wa cio df-riota iffalse
      cif syl5eq eleq1d mtbiri con4i impbii ) ABCEZABCFZCGZABCHUDUFUDIZUFCJKZCG
      CDLUGUEUHCUGUEUDBMCGANBOZUHRUHABCPUDUIUHQSTUAUBUC $.
      $( [15-Sep-2011] $)

    $( Restricted iota equals the undefined value of its domain of discourse
       ` A ` when not meaningful. $)
    riotaundb $p |- ( -. E! x e. A ph
                      <-> ( iota_ x e. A ph ) = ( Undef ` A ) ) $=
      ( wreu crio cund cfv wceq riotaund undefnel2 riotaclb eleq1 syl5bb mtbiri
      wn wcel impbii ) ABCEZPABCFZCGHZIZABCJUBSUACQZCDKSTCQUBUCABCDLTUACMNOR $.
      $( [26-Sep-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Closure of restricted iota. $)
    riotaclbg $p |- ( A e. V
          -> ( E! x e. A ph <-> ( iota_ x e. A ph ) e. A ) ) $=
      ( vy cv wreu crio wcel wceq reueq1 riotaeqdv eleq12d bibi12d vex riotaclb
      wb id vtoclg ) ABEFZGZABTHZTIZQABCGZABCHZCIZQECDTCJZUAUDUCUFABTCKUGUBUETC
      UGABTCUGRZLUHMNABTEOPS $.
      $( [28-Feb-2013] $)
  $}

  ${
    $d x ph $.
    riotabidva.1 $e |- ( ( ph /\ x e. A ) -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal restricted class abstractions (deduction
       rule).  ( ~ rabbidva analog.) $)
    riotabidva $p |- ( ph -> ( iota_ x e. A ps ) = ( iota_ x e. A ch ) ) $=
      ( wreu crio wceq wa cv wcel cio pm5.32da riotaiota adantl syl6bi riotaund
      imp wn iotabidv adantr reubidva 3eqtr4d cund cfv notbid eqtr4d pm2.61dan
      ) ABDEGZBDEHZCDEHZIAUJJDKELZBJZDMZUMCJZDMZUKULAUOUQIUJAUNUPDAUMBCFNUAUBUJ
      UKUOIABDEOPAUJULUQIZAUJCDEGZURABCDEFUCZCDEOQSUDAUJTZJUKEUEUFZULVAUKVBIABD
      ERPAVAULVBIZAVAUSTVCAUJUSUTUGCDERQSUHUI $.
      $( [17-Jan-2012] $)
  $}

  ${
    riotabiia.1 $e |- ( x e. A -> ( ph <-> ps ) ) $.
    $( Equivalent wff's yield equal restricted iotas (inference rule).
       ( ~ rabbiia analog.) $)
    riotabiia $p |- ( iota_ x e. A ph ) = ( iota_ x e. A ps ) $=
      ( cvv wceq crio eqid cv wcel wb adantl riotabidva ax-mp ) FFGZACDHBCDHGFI
      PABCDCJDKABLPEMNO $.
      $( [16-Jan-2012] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuuni4 . $)
  riota4 $p |- ( E! x e. A ph -> [ ( iota_ x e. A ph ) / x ] ph ) $=
    ( wreu crio wsbc cv wcel wa cio reiota4 wceq wb riotaiota dfsbcq syl mpbird
    ) ABCDZABABCEZFZABBGCHAIBJZFZABCKRSUALTUBMABCNABSUAOPQ $.
    $( [16-Sep-2011] $)

  ${
    $d x y A $.
    $( Membership law for "the unique element in ` A ` such that ` ph ` ."
       ( ~ reucl2 analog.) $)
    riotacl2 $p |- ( E! x e. A ph
            -> ( iota_ x e. A ph ) e. { x e. A | ph } ) $=
      ( vy wreu crio wcel wsbc crab riotacl riota4 cv ax-17 elrabsf sylanbrc )
      ABCEABCFZCGABPHPABCIGABCJABCKABDPCDLCGBMNO $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d y ph $.  $d y ps $.  $d x y A $.  $d y B $.
    riota2df.1 $e |- ( ph -> A. x ph ) $.
    riota2df.2 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    riota2df.3 $e |- ( ( ph /\ B e. A ) -> ( ch -> A. x ch ) ) $.
    riota2df.4 $e |- ( ph -> ( x = B -> ( ps <-> ch ) ) ) $.
    $( A deduction version of ~ riota2f . $)
    riota2df $p |- ( ( ph /\ B e. A /\ E! x e. A ps ) ->
            ( ch <-> ( iota_ x e. A ps ) = B ) ) $=
      ( wcel wreu w3a cv wa cio wceq crio reiota2df wb riotaiota eqeq1d bitr4d
      3ad2ant3 ) AGFLZBDFMZNCDOFLBPDQZGRZBDFSZGRZABCDEFGHIJKTUGAUKUIUAUFUGUJUHG
      BDFUBUCUEUD $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    riota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( This theorem shows a condition that allows us to represent a descriptor
       with a class expression ` B ` .  The second hypothesis is a weakened
       bound variable condition that allows ~ hbsbc1g to be used.  Compare
       ~ reuuni2f . $)
    riota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota_ x e. A ph ) = B ) ) $=
      ( wcel wreu wa cv cio wceq crio reiota2f wb riotaiota eqeq1d adantl
      bitr4d ) FEJZACEKZLBCMEJALCNZFOZACEPZFOZABCDEFGHIQUDUHUFRUCUDUGUEFACESTUA
      UB $.
      $( [11-Oct-2011] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    riotaprop.0 $e |- ( ps -> A. x ps ) $.
    riotaprop.1 $e |- B = ( iota_ x e. A ph ) $.
    riotaprop.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Properties of a restricted definite description operator.  Todo: can
       some uses of ~ riota2f be shortened with this? $)
    riotaprop $p |- ( E! x e. A ph -> ( B e. A /\ ps ) ) $=
      ( vy wreu wcel crio riotacl syl5eqel wa wceq eqcomi hbriota1 hbxfreq wal
      wi a1i riota2f mpbiri mpancom jca ) ACDJZEDKZBUGEACDLZDGACDMNZUHUGBUJUHUG
      OBUIEPEUIGQABCIDECIEUIGACIDRSBBCTUAUHFUBHUCUDUEUF $.
      $( [23-Nov-2013] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x ph $.  $d y ps $.
    riota5f.1 $e |- ( y e. B -> A. x y e. B ) $.
    riota5f.2 $e |- ( ( ph /\ B e. A /\ x e. A ) -> ( ps <-> x = B ) ) $.
    $( A method for computing restricted iota. $)
    riota5f $p |- ( ( ph /\ B e. A ) -> ( iota_ x e. A ps ) = B ) $=
      ( wcel wa wceq cv adantl wi wal ax-17 simpr wb mpbid alrimi wsbc crio wex
      cvv elex issetf sylib hbel hban simpll simplr eleq1a imp adantll biimparc
      w3a syl13anc sbceq1a hbsbc1g 19.23t syl mpd wreu wral wrex 3expia ralrimi
      ex hbeleq eqeq2 bibi2d ralbid rcla4ev syl2anc reu6 sylibr riota2f ) AFEIZ
      JZBCFUAZBCEUBFKZVSCLZFKZCUCZVTVRWDAVRFUDIWDFEUECDFGUFUGMVSWCVTNZCOZWDVTNZ
      VSWECAVRCACPCDDFEGDLZEICPUHZUIZVSWCVTVSWCJZBVTWKWCAVRWBEIZBVSWCQAVRWCUJAV
      RWCUKVRWCWLAVRWCWLFEWBULUMUNAVRWLUPBWCHUOUQWCBVTRVSBCFURZMSVHTVSVTVTCONZC
      OZWFWGRVRWOAVRWNCWIBCDFEGUSZTMWCVTCUTVASVBVSVRBCEVCZVTWARAVRQZVSBWBWHKZRZ
      CEVDZDEVEZWQVSVRBWCRZCEVDZXBWRVSXCCEWJAVRWLXCHVFVGXAXDDFEWHFKZWTXCCECDFGV
      IXEWSWCBWHFWBVJVKVLVMVNBCDEVOVPBVTCDEFGWPWMVQVNS $.
      $( [16-Apr-2013] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x ph $.  $d y ps $.
    riota5.1 $e |- ( ( ph /\ B e. A /\ x e. A ) -> ( ps <-> x = B ) ) $.
    $( A method for computing restricted iota. $)
    riota5 $p |- ( ( ph /\ B e. A ) -> ( iota_ x e. A ps ) = B ) $=
      ( vy cv wcel ax-17 riota5f ) ABCGDEGHEICJFK $.
      $( [20-Oct-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class.  Compare
       ~ reuuniss2 . $)
    riotass2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) )
         -> ( iota_ x e. A ph ) = ( iota_ x e. B ps ) ) $=
      ( wss wi wral wrex wreu wcel cio crio reiotass2 wceq reuss2 riotaiota syl
      wa cv ad2antll 3eqtr4d ) DEFABGCDHSZACDIZBCEJZSSZCTZDKASCLZUGEKBSCLZACDMZ
      BCEMZABCDENUFACDJUJUHOABCDEPACDQRUEUKUIOUCUDBCEQUAUB $.
      $( [22-Mar-2013] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d x y ph $.  $d ps y z $.
    $d ch x z $.
    riotaxfrd.1 $e |- ( z e. C -> A. y z e. C ) $.
    riotaxfrd.2 $e |- ( ( ph /\ y e. A ) -> B e. A ) $.
    riotaxfrd.3 $e |- ( ( ph /\ ( iota_ y e. A ch ) e. A ) -> C e. A ) $.
    riotaxfrd.4 $e |- ( x = B -> ( ps <-> ch ) ) $.
    riotaxfrd.5 $e |- ( y = ( iota_ y e. A ch ) -> B = C ) $.
    riotaxfrd.6 $e |- ( ( ph /\ x e. A ) -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` x ` such
       that ` ps ` " to another variable ` y ` contained in expression ` B ` .
       Use ~ reuhypd to eliminate the last hypothesis.  ( ~ reuunixfr
       analog.) $)
    riotaxfrd $p |- ( ( ph /\ E! x e. A ps ) -> ( iota_ x e. A ps ) = C ) $=
      ( wreu wa crio cv wcel crab rabid baib riotabiia wceq reuxfrd riotacl2 wb
      adantl riotacl hbriota1 rabxfrd sylan2 mpbird ex sylbid imp baibr reubiia
      syl5 biimpi ax-17 wal hbrab1 hbel a1i eleq1 riota2f syl2anc mpbid syl5eqr
      wi ) ABDGPZQZBDGRDSZBDGUAZTZDGRZIVQBDGVQVOGTZBBDGUBZUCUDVNIVPTZVRIUEZAVMW
      AAVMCEGPZWAABCDEHGKOMUFZAWCWAAWCQWACEGRZCEGUATZWCWFACEGUGUIWCAWEGTZWAWFUH
      CEGUJZABCDEFHWEIGCEFGUKJKMNULUMUNUOUPUQVNIGTZVQDGPZWAWBUHAVMWIAVMWCWIWDWC
      WGAWIWHAWGWILUOUTUPUQVMWJAVMWJBVQDGVQVSBVTURUSVAUIVQWADFGIFSITDVBZWAWADVC
      VLWIDFFIVPWKBDFGVDVEVFVOIVPVGVHVIVJVK $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasvd.1 $e |- ( ph -> A. x ph ) $.
    riotasvd.2 $e |- ( ph -> A. y ph ) $.
    riotasvd.3 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasvd.4 $e |- ( ph -> D e. A ) $.
    $( Deduction version of ~ riotasv . $)
    riotasvd $p |- ( ( ph /\ A e. V ) -> ( ( y e. B /\ ps ) -> D = C ) ) $=
      ( vz wcel wceq wi adantr wal a17d wa cv wral crio wreu wb simpl riotaclbg
      eqcomd eleq1d biimprd adantl sylbid mpd a1i hbxfrd hbeqd hbimd hbrald a1d
      hbriota1 hbra1 hbriotad hband anabsi5 eqeq1 imbi2d ralbid riota2df mpbird
      ex syl3anc ra4 syl imp3a ) AEIOZUAZDUBFOZBHGPZVQBVSQZDFUCZVRVTQVQWABCUBZG
      PZQZDFUCZCEUDZHPZAWGVPAHWFLUIRVQAHEOZWECEUEZWAWGUFAVPUGAWHVPMRZVQWHWIWJVQ
      WHWFEOZWIAWHWKUFVPAHWFELUJRVPWKWIQAVPWIWKWECEIUHUKULUMUNAWEWACNEHJACNHWFJ
      LNUBZWFOZWMCSQAWECNEVAUOUPZAWAWACSQWHAVTCDFJKAVRCTABVSCJABCTACNNHGJWNAWLG
      OCTUQURUSRAWBHPZWEWAUFAWOUAZWDVTDFAWOWPDSAAWODAADSAKUTADNNWBHKAWLWBODTADN
      HWFKLAWEDCNEKJWEWEDSQAWDDFVBUOAWLEODTVCUPUQVDVEWPWCVSBWOWCVSUFAWBHGVFULVG
      VHVKVIVLVJVTDFVMVNVO $.
      $( [4-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasvdOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasvdOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasvdOLD.3 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    $( Deduction version of ~ riotasv . $)
    riotasvdOLD $p |- ( ( ( ph /\ A e. V ) /\ D e. A /\ ( y e. B /\ ps ) )
         -> D = C ) $=
      ( vz wcel wa wceq wi wb wal a17d cv wral crio eqcomd ad2antrr wreu eleq1d
      adantr riotaclbg biimprd adantl sylbid hbriota1 hbxfrd hbeqd hbimd hbrald
      imp a1i a1d hbra1 hbriotad hband eqeq1 imbi2d ralbid ex riota2df 3adant1r
      anabsi5 mpd3an3 mpbird ra4 syl6 imp4a 3imp ) AEINZOZHENZDUAFNZBOHGPZVRVSV
      TBWAVRVSBWAQZDFUBZVTWBQVRVSWCVRVSOWCBCUAZGPZQZDFUBZCEUCZHPZAWIVQVSAHWHLUD
      UEVRVSWGCEUFZWCWIRZVRVSWJVRVSWHENZWJAVSWLRVQAHWHELUGUHVQWLWJQAVQWJWLWGCEI
      UIUJUKULURAVSWJWKVQAWGWCCMEHJACMHWHJLMUAZWHNZWNCSQAWGCMEUMUSUNZAWCWCCSQVS
      AWBCDFJKAVTCTABWACJABCTACMMHGJWOAWMGNCTUOUPUQUHAWDHPZWGWCRAWPOZWFWBDFAWPW
      QDSAAWPDAADSAKUTADMMWDHKAWMWDNDTADMHWHKLAWGDCMEKJWGWGDSQAWFDFVAUSAWMENDTV
      BUNUOVCVJWQWEWABWPWEWARAWDHGVDUKVEVFVGVHVIVKVLVGWBDFVMVNVOVP $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d y z E $.
    $d z F $.  $d z ph $.  $d x z ps $.
    riotasv2d.1 $e |- ( ph -> A. x ph ) $.
    riotasv2d.2 $e |- ( ph -> A. y ph ) $.
    riotasv2d.3 $e |- ( ph -> ( z e. F -> A. y z e. F ) ) $.
    riotasv2d.4 $e |- ( ph -> ( ch -> A. y ch ) ) $.
    riotasv2d.5 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv2d.6 $e |- ( ph -> ( y = E -> ( ps <-> ch ) ) ) $.
    riotasv2d.7 $e |- ( ph -> ( y = E -> C = F ) ) $.
    riotasv2d.8 $e |- ( ph -> D e. A ) $.
    riotasv2d.9 $e |- ( ph -> E e. B ) $.
    riotasv2d.10 $e |- ( ph -> ch ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv2d $p |- ( ( ph /\ A e. V ) -> D = F ) $=
      ( wcel cvv wceq elex wa adantr cv wal wi wb ax-17 gen2 a1i a17d wral crio
      hband hbra1 hbriotad hbxfrd hbeqd hbimd alrimi adantl imp anbi12d imbi12d
      eleq1 eqeq2d ex hban riotasvd vtoclgft syl221anc mp2and sylan2 ) GMUDAGUE
      UDZJLUFZGMUGAVTUHZKHUDZCWAAWCVTUBUIZACVTUCUIWBFUJZKUDZWFEUKULZFUKEUKZWCCU
      HZWAULZWJEUKULZEUKZEUJZKUFZWMHUDZBUHZJIUFZULZWJUMZULZEUKZWREUKWCWJWHWBWGE
      FWFEUNUOUPAWLVTAWKEOAWIWAEOAWCCEAWCEUQQUTAEFFJLOAEFJBDUJIUFULZEHURZDGUSOR
      AXCEDFGONXCXCEUKULAXBEHVAUPAWEGUDEUQVBVCPVDVEVFUIAXAVTAWTEOAWNWSAWNUHZWPW
      IWQWAXDWOWCBCWNWOWCUMAWMKHVKVGAWNBCUMSVHVIXDILJAWNILUFTVHVLVJVMVFUIWBWREA
      VTEOVTEUNVNABDEGHIJUENORUAVOVFWDWRWJEFKHVPVQVRVS $.
      $( [2-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d y z E $.
    $d z F $.  $d z ph $.  $d x z ps $.
    riotasv2dOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasv2dOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasv2dOLD.3 $e |- ( ph -> ( z e. F -> A. y z e. F ) ) $.
    riotasv2dOLD.4 $e |- ( ph -> ( ch -> A. y ch ) ) $.
    riotasv2dOLD.5 $e |- ( ph
        -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv2dOLD.6 $e |- ( ph -> ( y = E -> ( ps <-> ch ) ) ) $.
    riotasv2dOLD.7 $e |- ( ph -> ( y = E -> C = F ) ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv2dOLD $p |- ( ( ( ph /\ A e. V ) /\ ( D e. A /\ E e. B /\ ch ) )
             -> D = F ) $=
      ( wcel cvv w3a wceq elex wa wi cv wal ax-17 gen2 a1i wral crio hbra1 a17d
      hbriotad hbxfrd hbeld hb3and hbeqd hbimd alrimi ad2antrr eleq1 adantl imp
      wb 3anbi23d eqeq2d imbi12d hban riotasvdOLD 3exp exp4a 3impd adantr simpr
      ex vtoclgft syl221anc 3expd com23 pm2.43d 3imp2 sylanl2 ) GMUAAGUBUAZJGUA
      ZKHUAZCUCZJLUDZGMUEAWGUFZWHWICWKWLWIWHCWKUGZWLWIWHWMUGZWLWIWIWNUGWLWIUFZW
      HWIWMWOWHWICWKWOFUHZKUAZWQEUIUGZFUIEUIZWJWKUGZWTEUIUGZEUIZEUHZKUDZWHXCHUA
      ZBUCZJIUDZUGZWTVHZUGZEUIZXHEUIZWIWTWSWOWREFWQEUJUKULAXBWGWIAXAEOAWJWKEOAW
      HWICEAEFJGOAEFJBDUHIUDUGZEHUMZDGUNORAXNEDFGONXNXNEUIUGAXMEHUOULAWPGUAEUPZ
      UQURZXOUSAWIEUPQUTAEFFJLOXPPVAVBVCVDAXKWGWIAXJEOAXDXIAXDUFZXFWJXGWKXQXEWI
      BCWHXDXEWIVHAXCKHVEVFAXDBCVHSVGVIXQILJAXDILUDTVGVJVKVSVCVDWLXLWIWLXHEAWGE
      OWGEUJVLWLWHXEBXGWLWHXEBXGWLWHXEBUFXGABDEGHIJUBNORVMVNVOVPVCVQWLWIVRXHWTE
      FKHVTWAWBWCVSWDWCWEWF $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.  $d z D $.  $d x y z E $.
    $d x z ph $.
    riotasv2s.2 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    $( The value of description binder ` D ` for a single-valued class
       expression ` C ( y ) ` (as in e.g. ~ reusv2 ) in the form of a
       substitution instance.  Special case of ~ riota2f . $)
    riotasv2s $p |- ( ( A e. V /\ D e. A /\ ( E e. B /\ [ E / y ] ph ) )
            -> D = [_ E / y ]_ C ) $=
      ( vz wcel wa wceq cv wi hbxfreq ax-17 wal a1i wsbc 3simpc simp1 wral crio
      w3a csb hbriota1 hbel hban hbra1 hbriota a17d hbsbc1g hband exp4d pm2.43b
      imp32 hbcsb1g ad2antrl wb sbceq1a csbeq1a simprl simprr riotasv2d syl2anc
      simpl ) DILZGDLZHELZACHUAZMZUFVJVMMZVIGCHFUGZNVIVJVMUBVIVJVMUCVNAVLBCKDEF
      GHVOIVJVMBBKKGDBKGABOFNPZCEUDZBDUEZJVQBKDUHQKOZDLZBRUIVMBRUJVJVKVLVNCSZVJ
      VKVLWAPVKVJVKVLWAVKVJVMCVJVJCSPVKCKKGDCKGVRJVQCBKDVPCEUKVTCRZULQWBUITVKVK
      VLCVKVKCUMACKHEVSHLCRZUNZUOUOUPUQURVKVSVOLZWECSPVJVLCKHFEWCUSUTVKVLVLCSPV
      JVLWDUTGVRNVNJTCOHNZAVLVAPVNACHVBTWFFVONPVNCHFVCTVJVMVHVJVKVLVDVJVKVLVEVF
      VG $.
      $( [3-Mar-2013] $)

    $( The value of description binder ` D ` for a single-valued class
       expression ` C ( y ) ` (as in e.g. ~ reusv2 ) in the form of a
       substitution instance.  Special case of ~ riota2f . $)
    riotasv2sOLD $p |- ( ( A e. V /\ D e. A /\ ( E e. B /\ [ E / y ] ph ) )
            -> D = [_ E / y ]_ C ) $=
      ( vz wcel wsbc wa w3a wceq ax-17 cv wi a1i csb simp3l simp1 simp2 hbcsb1g
      simp3r hbsbc1g wral crio wb sbceq1a csbeq1a riotasv2dOLD syl23anc ) DILZG
      DLZHELZACHMZNZOUQUOUPUQURGCHFUAZPUOUPUQURUBZUOUPUSUCUOUPUSUDVAUOUPUQURUFU
      QAURBCKDEFGHUTIUQBQUQCQCKHFEKRHLCQZUEACKHEVBUGGABRFPSCEUHBDUIPUQJTCRHPZAU
      RUJSUQACHUKTVCFUTPSUQCHFULTUMUN $.
      $( [1-Feb-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d x z ph $.  $d z D $.
    riotasv.1 $e |- A e. _V $.
    riotasv.2 $e |- D = ( iota_ x e. A A. y e. B ( ph -> x = C ) ) $.
    $( Value of description binder ` D ` for a single-valued class expression
       ` C ( y ) ` (as in e.g. ~ reusv2 ).  Special case of ~ riota2f . $)
    riotasv $p |- ( ( D e. A /\ y e. B /\ ph ) -> D = C ) $=
      ( vz wcel cv wceq cvv wa wi wral hbxfreq ax-17 hbel crio hbriota1 hbriota
      hbra1 a1i id riotasvd mpan2 3impib ) GDKZCLEKZAGFMZUJDNKUKAOULPHUJABCDEFG
      NBJJGDBJGABLFMPZCEQZBDUAZIUNBJDUBRJLDKZBSTCJJGDCJGUOIUNCBJDUMCEUDUPCSZUCR
      UQTGUOMUJIUEUJUFUGUHUI $.
      $( [26-Jan-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasv3d.1 $e |- ( ph -> A. x ph ) $.
    riotasv3d.2 $e |- ( ph -> A. y ph ) $.
    riotasv3d.3 $e |- ( ph -> ( th -> A. y th ) ) $.
    riotasv3d.4 $e |- ( ph
           -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv3d.5 $e |- ( ph -> ( C = D -> ( ch <-> th ) ) ) $.
    riotasv3d.6 $e |- ( ph -> ( ( y e. B /\ ps ) -> ch ) ) $.
    riotasv3d.7 $e |- ( ph -> D e. A ) $.
    riotasv3d.8 $e |- ( ph -> E. y e. B ps ) $.
    $( A property ` ch ` holding for a representative of a single-valued class
       expression ` C ( y ) ` (see e.g. ~ reusv2 ) also holds for its
       description binder ` D ` (in the form of property ` th ` ). $)
    riotasv3d $p |- ( ( ph /\ A e. V ) -> th ) $=
      ( wa wcel cvv elex wrex adantr wi wral ax-17 cv imp adantrl wceq riotasvd
      wb eqcom syl6ib expimpd mpbid exp45 ralrimd wal alrimi r19.23t syl sylibd
      syld mpd sylan2 ) GKUAAGUBUAZDGKUCAVITZBFHUDZDAVKVISUEAVIVKDUFZAVIBDUFZFH
      UGZVLAVIVMFHMVIFUHAVIFUIHUAZBDAVIVOBTZTZTCDAVPCVIAVPCQUJUKAVQCDUNZAVQIJUL
      ZVRAVIVPVSVJVPJIULVSABEFGHIJUBLMORUMJIUOUPUQPVFUJURUSUTADDFVAUFZFVAVNVLUN
      AVTFMNVBBDFHVCVDVEUJVGVH $.
      $( [5-Mar-2013] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d z D $.  $d z ph $.
    $d x z ps $.
    riotasv3dOLD.1 $e |- ( ph -> A. x ph ) $.
    riotasv3dOLD.2 $e |- ( ph -> A. y ph ) $.
    riotasv3dOLD.3 $e |- ( ph -> ( th -> A. y th ) ) $.
    riotasv3dOLD.4 $e |- ( ph
           -> D = ( iota_ x e. A A. y e. B ( ps -> x = C ) ) ) $.
    riotasv3dOLD.5 $e |- ( ph -> ( C = D -> ( ch <-> th ) ) ) $.
    riotasv3dOLD.6 $e |- ( ph -> ( ( y e. B /\ ps ) -> ch ) ) $.
    $( A property ` ch ` holding for a representative of a single-valued class
       expression ` C ( y ) ` (see e.g. ~ reusv2 ) also holds for its
       description binder ` D ` (in the form of property ` th ` ). $)
    riotasv3dOLD $p |- ( ( ph /\ ( A e. V /\ D e. A /\ E. y e. B ps ) )
          -> th ) $=
      ( wcel wi wa vz wrex cvv elex wral cv imp adantrl wb wceq w3a riotasvdOLD
      eqcomd 3exp ex imp4c syld mpbid exp45 com23 ralrimi a17d crio hbra1 ax-17
      wal hbriota a1i hbxfrd hbeld hband alrimi r19.21t syl r19.23t sylibd syl5
      exp3a 3imp2 ) AGKRZJGRZBFHUBZDVTGUCRZAWAWBDSZSGKUDAWCWAWDAWCWATZBDSZFHUEZ
      WDAWEWFSZFHUEZWEWGSZAWHFHMAWEFUFHRZWFAWEWKBDAWEWKBTZTZTCDAWLCWEAWLCQUGUHA
      WMCDUIZAWMIJUJZWNAWCWAWLWOAWCWAWLWOSSAWCTZWAWLWOWPWAWLUKJIABEFGHIJUCLMOUL
      UMUNUOUPPUQUGURUSUTVAAWEWEFVFSZFVFWIWJUIAWQFMAWCWAFAWCFVBAFUAJGMAFUAJBEUF
      IUJSZFHUEZEGVCZMOUAUFZWTRZXBFVFSAWSFEUAGWRFHVDXAGRZFVEVGVHVIAXCFVBVJVKVLW
      EWFFHVMVNURADDFVFSZFVFWGWDUIAXDFMNVLBDFHVOVNVPVRVQVS $.
      $( [1-Feb-2013] $)
  $}


