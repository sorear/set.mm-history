$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        The natural numbers (i.e. finite ordinals)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Declare new symbol. $)
  $c om $. $( Greek letter omega $)

  $( Extend class notation to include the class of natural numbers. $)
  com $a class om $.

  ${
    $d x y $.
    $( Define the class of natural numbers, which are all ordinal numbers that
       are less than every limit ordinal, i.e. all finite ordinals.  Our
       definition is a variant of the Definition of N of [BellMachover]
       p. 471.  See ~ dfom2 for an alternate definition.  Later, when we assume
       the Axiom of Infinity, we show ` om ` is a set in ~ omex , and ` om `
       can then be defined per ~ dfom3 (the smallest inductive set) and
       ~ dfom4 .

       _Note_: the natural numbers ` om ` are a subset of the ordinal numbers
       ~ df-on .  They are completely different from the natural numbers ` NN `
       ( ~ df-n ) that are a subset of the complex numbers defined much later
       in our development, although the two sets have analogous properties and
       operations defined on them. $)
    df-om $a |- om = { x | ( Ord x /\ A. y ( Lim y -> x e. y ) ) } $.
  $}

  ${
    $d x z $.  $d y z $.
    $( An alternate definition of the set of natural numbers ` om ` .
       Definition 7.28 of [TakeutiZaring] p. 42, who use the symbol K_I for the
       inner class builder of non-limit ordinal numbers (see ~ nlimon ). $)
    dfom2 $p |- om = { x e. On | suc x C_ { y e. On | -. Lim y } } $=
      ( vz cv word wlim wcel wi wal wa cab con0 csuc wn crab wss vex cvv impexp
      wb com elon anbi1i onsssuc ontri1 bitr3d ancoms wceq limeq notbid imbi12d
      elrab a1i pm5.74da limelon mpan pm4.71ri imbi1i con34b ibar imbi2d syl5bb
      pm5.74i 3bitri syl6rbbr simpr suceloni onelon ex syl ancrd impbid2 imbi1d
      syl5bbr bitrd albidv dfss2 syl6bbr pm5.32i bitr3i abbii df-rab 3eqtr4i
      df-om ) ADZEZCDZFZWEWGGZHZCIZJZAKWELGZWEMZBDZFZNZBLOZPZJZAKUAWSALOWLWTAWL
      WMWKJWTWMWFWKWEAQUBUCWMWKWSWMWKWGWNGZWGWRGZHZCIWSWMWJXCCWMWJWGLGZXCHZXCWM
      XEXDWINZXDWHNZJZHZHZWJWMXDXCXIWMXDJZXAXFXBXHXDWMXAXFTXDWMJWGWEPXAXFWGWEUD
      WGWEUEUFUGXBXHTXKWQXGBWGLWOWGUHWPWHWOWGUIUJULUMUKUNWJXDWHJZWIHXDWJHXJWHXL
      WIWHXDWGRGWHXDCQWGRUOUPUQURXDWHWISXDWJXIWJXFXGHXDXIWHWIUSXDXGXHXFXDXGUTVA
      VBVCVDVEXEXDXAJZXBHWMXCXDXAXBSWMXMXAXBWMXMXAXDXAVFWMXAXDWMWNLGZXAXDHWEVGX
      NXAXDWNWGVHVIVJVKVLVMVNVOVPCWNWRVQVRVSVTWAACWDWSALWBWC $.
      $( [1-Nov-2004] $)
  $}

  ${
    $d A x y $.
    elom.1 $e |- A e. _V $.
    $( Membership in omega.  The hypothesis can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 . $)
    elom $p |- ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) $=
      ( vy cv word wlim wcel wi wal wa com wceq ordeq eleq1 imbi2d albidv df-om
      anbi12d elab2 ) DEZFZAEZGZUAUCHZIZAJZKBFZUDBUCHZIZAJZKDBLCUABMZUBUHUGUKUA
      BNULUFUJAULUEUIUDUABUCOPQSDART $.
      $( [15-May-1994] $)
  $}

  ${
    $d A x y $.
    $( Membership in omega.  The antecedent can be eliminated if we assume the
       Axiom of Infinity; see ~ elom3 . $)
    elomg $p |- ( A e. V ->
                ( A e. om <-> ( Ord A /\ A. x ( Lim x -> A e. x ) ) ) ) $=
      ( vy cv com wcel word wlim wi wal wa eleq1 wceq imbi2d albidv anbi12d vex
      ordeq elom vtoclbg ) DEZFGUBHZAEZIZUBUDGZJZAKZLBFGBHZUEBUDGZJZAKZLDBCUBBF
      MUBBNZUCUIUHULUBBSUMUGUKAUMUFUJUEUBBUDMOPQAUBDRTUA $.
      $( [25-Mar-1995] $)
  $}

  ${
    $d x y $.
    $( Omega is a subset of ` On ` .  (The proof was shortened by Andrew
       Salmon, 27-Aug-2011.) $)
    omsson $p |- om C_ On $=
      ( vx vy com cv csuc wlim wn con0 crab wss dfom2 ssrab2 eqsstri ) CADEBDFG
      BHIJZAHIHABKNAHLM $.
      $( [27-Aug-2011] $) $( [13-Jun-1994] $)
  $}

  ${
    $d x y A $.
    $( The class of natural numbers is a subclass of any (infinite) limit
       ordinal.  Exercise 1 of [TakeutiZaring] p. 44.  Remarkably, our proof
       does not require the Axiom of Infinity. $)
    limomss $p |- ( Lim A -> om C_ A ) $=
      ( vx vy word wlim com wss limord con0 wcel wceq wo wi ordeleqon wa cv wal
      vex elom simprbi limeq eleq2 imbi12d cla4gv syl5 com23 imp ssrdv ex sseq2
      omsson mpbiri a1d jaoi sylbi mpcom ) ADZAEZFAGZAHUQAIJZAIKZLURUSMZANUTVBV
      AUTURUSUTUROBFAUTURBPZFJZVCAJZMUTVDURVEVDCPZEZVCVFJZMZCQZUTURVEMZVDVCDVJC
      VCBRSTVIVKCAIVFAKVGURVHVEVFAUAVFAVCUBUCUDUEUFUGUHUIVAUSURVAUSFIGUKAIFUJUL
      UMUNUOUP $.
      $( [30-Oct-2003] $)
  $}

  $( A natural number is an ordinal number. $)
  nnon $p |- ( A e. om -> A e. On ) $=
    ( com con0 omsson sseli ) BCADE $.
    $( [27-Jun-1994] $)

  ${
    nnoni.1 $e |- A e. om $.
    $( A natural number is an ordinal number. $)
    nnoni $p |- A e. On $=
      ( com wcel con0 nnon ax-mp ) ACDAEDBAFG $.
      $( [27-Jun-1994] $)
  $}

  $( A natural number is ordinal. $)
  nnord $p |- ( A e. om -> Ord A ) $=
    ( com wcel con0 word nnon eloni syl ) ABCADCAEAFAGH $.
    $( [17-Oct-1995] $)

  ${
    $d x y z $.
    $( Omega is ordinal.  Theorem 7.32 of [TakeutiZaring] p. 43.  (The proof
       was shortened by Andrew Salmon, 27-Aug-2011.) $)
    ordom $p |- Ord om $=
      ( vy vx vz com wtr con0 wss word cv wcel wa wi dftr2 wlim ordelord expcom
      wal limord vex elom ordtr trel exp3a com12 a2d alimdv anim12d 3imtr4g imp
      3syl ax-gen mpgbir omsson ordon trssord mp3an ) DEZDFGFHDHUQAIZBIZJZUSDJZ
      KURDJZLZBQAABDMVCBUTVAVBUTUSHZCIZNZUSVEJZLZCQZKURHZVFURVEJZLZCQZKVAVBUTVD
      VJVIVMVDUTVJUSUROPUTVHVLCUTVFVGVKVFUTVGVKLVFUTVGVKVFVEHVEEUTVGKVKLVERVEUA
      VEURUSUBUJUCUDUEUFUGCUSBSTCURASTUHUIUKULUMUNDFUOUP $.
      $( [18-Oct-1995] $)
  $}

  $( A member of a natural number is a natural number. $)
  elnn $p |- ( ( A e. B /\ B e. om ) -> A e. om ) $=
    ( com word wtr wcel wa wi ordom ordtr trel mp2b ) CDCEABFBCFGACFHICJCABKL
    $.
    $( [21-Jun-1998] $)

  $( The class of natural numbers ` om ` is either an ordinal number (if we
     accept the Axiom of Infinity) or the proper class of all ordinal numbers
     (if we deny the Axiom of Infinity).  Remark in [TakeutiZaring] p. 43. $)
  omon $p |- ( om e. On \/ om = On ) $=
    ( com word con0 wcel wceq wo ordom ordeleqon mpbi ) ABACDACEFGAHI $.
    $( [10-May-1998] $)

  ${
    $( Omega is an ordinal number.  (Contributed by Mario Carneiro,
       9-Feb-2013.) $)
    omelon2 $p |- ( om e. _V -> om e. On ) $=
      ( com con0 wcel cvv wn wceq omon ori onprc eleq1 mtbiri syl con4i ) ABCZA
      DCZNEABFZOENPGHPOBDCIABDJKLM $.
      $( [30-Jan-2013] $)
  $}

  ${
    $d x y A $.
    $( A natural number is not a limit ordinal. $)
    nnlim $p |- ( A e. om -> -. Lim A ) $=
      ( vx com wcel wlim word wn nnord ordirr syl cv wi wal wa elomg ibi simprd
      wceq limeq eleq2 imbi12d cla4gv mpd mtod ) ACDZAEZAADZUEAFZUGGAHAIJUEBKZE
      ZAUIDZLZBMZUFUGLZUEUHUMUEUHUMNBACOPQULUNBACUIARUJUFUKUGUIASUIAATUAUBUCUD
      $.
      $( [18-Oct-1995] $)

    $( The class of natural numbers is a subclass of the class of non-limit
       ordinal numbers.  Exercise 4 of [TakeutiZaring] p. 42.  (The proof was
       shortened by Andrew Salmon, 27-Aug-2011.) $)
    omssnlim $p |- om C_ { x e. On | -. Lim x } $=
      ( com cv wlim wn con0 crab wss wral ssrab omsson nnlim rgen mpbir2an ) BA
      CZDEZAFGHBFHPABIPAFBJKPABOLMN $.
      $( [27-Aug-2011] $) $( [2-Nov-2004] $)
  $}

  $( Omega is a limit ordinal.  Theorem 2.8 of [BellMachover] p. 473.  Our
     proof, however, does not require the Axiom of Infinity. $)
  limom $p |- Lim om $=
    ( vx com word wlim ordom con0 wcel wceq wo ordeleqon wn ordirr ax-mp cv wal
    wi wa limord biimpd limeq wss wb ordtri1 adantr ordsseleq nnlim a1i con3rr3
    jaod sylan9 sylbird con4d sylan ex pm2.43b alrimiv jctil elomg syl5ibr mt3i
    mpanl2 limon mpbiri jaoi sylbi ) BCZBDZEVFBFGZBFHZIVGBJVHVGVIVHVGBBGZVFVJKE
    BLMVGKZVJVHVFANZDZBVLGZPZAOZQVKVPVFVKVOAVKVMVNVMVKVOVMVLCZVKVOVLRVQVFVKVOEV
    QVFQZVKQZVNVMVSVNKZVLBUAZVMKZVRWAVTUBVKVLBUCUDVRWAVLBGZVLBHZIZVKWBVRWAWEVLB
    UESVKWCWBWDWCWBPVKVLUFUGWDVMVGWDVMVGVLBTSUHUIUJUKULVAUMUNUOUPEUQABFURUSUTVI
    VGFDVBBFTVCVDVEM $.
    $( [26-Mar-1995] $)

  $( A class belongs to omega iff its successor does. $)
  peano2b $p |- ( A e. om <-> suc A e. om ) $=
    ( com wlim wcel csuc wb limom limsuc ax-mp ) BCABDAEBDFGBAHI $.
    $( [3-Dec-1995] $)

  ${
    $d x y A $.
    $( A nonzero natural number is a successor. $)
    nnsuc $p |- ( ( A e. om /\ A =/= (/) ) -> E. x e. om A = suc x ) $=
      ( com wcel c0 wne wa cv csuc wceq con0 wrex wlim wn nnlim adantr wi nnord
      word cuni orduninsuc w3a df-lim biimpri 3expia sylbird sylan mt3d biimpcd
      wb eleq1 peano2b syl6ibr ancrd adantld reximdv2 mpd ) BCDZBEFZGZBAHZIZJZA
      KLZVCACLZUTVDBMZURVFNUSBOPURBSZUSVDNZVFQBRVGUSGVHBBTJZVFVGVIVHUJUSABUAPVG
      USVIVFVFVGUSVIUBBUCUDUEUFUGUHURVDVEQUSURVCVCAKCURVCVACDZVCGVAKDURVCVJURVC
      VBCDZVJVCURVKBVBCUKUIVAULUMUNUOUPPUQ $.
      $( [6-Apr-2007] $) $( [18-Feb-2004] $)
  $}

  ${
    $d x A $.
    $( An ordinal subclass of non-limit ordinals is a class of natural
       numbers.  Exercise 7 of [TakeutiZaring] p. 42. $)
    ssnlim $p |- ( ( Ord A /\ A C_ { x e. On | -. Lim x } ) -> A C_ om ) $=
      ( word cv wlim wn con0 crab wss wa com wcel limom ssel limeq notbid elrab
      wceq simprbi syl6 mt2i adantl wb ordom ordtri1 mpan2 adantr mpbird ) BCZB
      ADZEZFZAGHZIZJBKIZKBLZFZUNUQUIUNUPKEZMUNUPKUMLZURFZBUMKNUSKGLUTULUTAKGUJK
      RUKURUJKOPQSTUAUBUIUOUQUCZUNUIKCVAUDBKUEUFUGUH $.
      $( [2-Nov-2004] $)
  $}

