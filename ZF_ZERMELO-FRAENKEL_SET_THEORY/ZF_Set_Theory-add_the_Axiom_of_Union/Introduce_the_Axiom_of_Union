$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                  Introduce the Axiom of Union
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d w x y z $.
    $( Axiom of Union.  An axiom of Zermelo-Fraenkel set theory.  It states
       that a set ` y ` exists that includes the union of a given set ` x `
       i.e. the collection of all members of the members of ` x ` .  The
       variant ~ axun2 states that the union itself exists.  A version with the
       standard abbreviation for union is ~ uniex2 .  A version using class
       notation is ~ uniex .

       The union of a class ~ df-uni should not be confused with the union of
       two classes ~ df-un .  Their relationship is shown in ~ unipr . $)
    ax-un $a |- E. y A. z ( E. w ( z e. w /\ w e. x ) -> z e. y ) $.

    $( Axiom of Union expressed with fewest number of different variables. $)
    zfun $p |- E. x A. y ( E. x ( y e. x /\ x e. z ) -> y e. x ) $=
      ( vw cv wcel wa wex wi wal ax-un wceq elequ2 elequ1 anbi12d cbvexv imbi1i
      albii exbii mpbi ) BEZDEZFZUBCEZFZGZDHZUAAEZFZIZBJZAHUIUHUDFZGZAHZUIIZBJZ
      AHCABDKUKUPAUJUOBUGUNUIUFUMDAUBUHLUCUIUEULDABMDACNOPQRST $.
      $( [14-Aug-2003] $)

    $( A variant of the Axiom of Union ~ ax-un .  For any set ` x ` , there
       exists a set ` y ` whose members are exactly the members of the members
       of ` x ` i.e. the union of ` x ` .  Axiom Union of [BellMachover]
       p. 466. $)
    axun2 $p |- E. y A. z ( z e. y <-> E. w ( z e. w /\ w e. x ) ) $=
      ( cv wcel wa wex ax-un bm1.3ii ) CEDEZFKAEFGDHBCABCDIJ $.
      $( [4-Jun-2006] $)

    $( The Axiom of Union using the standard abbreviation for union.  Given any
       set ` x ` , its union ` y ` exists. $)
    uniex2 $p |- E. y y = U. x $=
      ( vz cv cuni wceq wex wcel wb wi wa zfun eluni imbi1i albii exbii bm1.3ii
      wal mpbir dfcleq ) BDZADZEZFZBGCDZUAHZUEUCHZICRZBGUGBCUGUFJZCRZBGUFUAUBHK
      BGZUFJZCRZBGBCALUJUMBUIULCUGUKUFBUEUBMNOPSQUDUHBCUAUCTPS $.
      $( [4-Jun-2006] $)
  $}

  ${
    $d x y A $.
    uniex.1 $e |- A e. _V $.
    $( The Axiom of Union in class notation.  This says that if ` A ` is a set
       i.e. ` A e. _V ` (see ~ isset ), then the union of ` A ` is also a set.
       Same as Axiom 3 of [TakeutiZaring] p. 16. $)
    uniex $p |- U. A e. _V $=
      ( vx vy cv cuni cvv wcel wceq unieq eleq1d uniex2 issetri vtocl ) CEZFZGH
      AFZGHCABOAIPQGOAJKDPCDLMN $.
      $( [11-Aug-1993] $)
  $}

  ${
    $d x A $.
    $( The ZF Axiom of Union in class notation, in the form of a theorem
       instead of an inference.  We use the antecedent ` A e. V ` instead of
       ` A e. _V ` to make the theorem more general and thus shorten some
       proofs; obviously the universal class constant ` _V ` is one possible
       substitution for class variable ` V ` . $)
    uniexg $p |- ( A e. V -> U. A e. _V ) $=
      ( vx cv cuni cvv wcel wceq unieq eleq1d vex uniex vtoclg ) CDZEZFGAEZFGCA
      BNAHOPFNAIJNCKLM $.
      $( [25-Nov-1994] $)
  $}

  ${
    unex.1 $e |- A e. _V $.
    unex.2 $e |- B e. _V $.
    $( The union of two sets is a set.  Corollary 5.8 of [TakeutiZaring]
       p. 16. $)
    unex $p |- ( A u. B ) e. _V $=
      ( cpr cuni cun cvv unipr prex uniex eqeltrri ) ABEZFABGHABCDIMABJKL $.
      $( [1-Jul-1994] $)
  $}

  $( A triple of classes exists. $)
  tpex $p |- { A , B , C } e. _V $=
    ( ctp cpr csn cun cvv df-tp prex snex unex eqeltri ) ABCDABEZCFZGHABCINOABJ
    CKLM $.
    $( [10-Apr-1994] $)

  ${
    $d x y A $.  $d x y B $.
    $( Existence of union is equivalent to existence of its components. $)
    unexb $p |- ( ( A e. _V /\ B e. _V ) <-> ( A u. B ) e. _V ) $=
      ( vx vy cvv wcel wa cun cv wceq uneq1 eleq1d uneq2 vex unex vtocl2g ssun1
      wss ssexg mpan ssun2 jca impbii ) AEFZBEFZGABHZEFZCIZDIZHZEFAUIHZEFUGCDAB
      EEUHAJUJUKEUHAUIKLUIBJUKUFEUIBAMLUHUICNDNOPUGUDUEAUFRUGUDABQAUFESTBUFRUGU
      EBAUABUFESTUBUC $.
      $( [11-Jun-1998] $)
  $}

  $( A union of two sets is a set.  Corollary 5.8 of [TakeutiZaring] p. 16. $)
  unexg $p |- ( ( A e. V /\ B e. W ) -> ( A u. B ) e. _V ) $=
    ( wcel cvv cun elex wa unexb biimpi syl2an ) ACEAFEZBFEZABGFEZBDEACHBDHMNIO
    ABJKL $.
    $( [18-Sep-2006] $)

  ${
    $( A version of ~ unisn without the ` A e. _V ` hypothesis.  (Contributed
       by Stefan Allan, 14-Mar-2006.) $)
    unisn2 $p |- U. { A } e. { (/) , A } $=
      ( cvv wcel csn cuni c0 cpr unisng wceq wo eqid elprg mpbiri eqeltrd snprc
      olci wn biimpi unieqd uni0 0ex prid1 eqeltri syl6eqel pm2.61i ) ABCZADZEZ
      FAGZCUFUHAUIABHUFAUICAFIZAAIZJUKUJAKPAFABLMNUFQZUHFEZUIULUGFULUGFIAORSUMF
      UITFAUAUBUCUDUE $.
      $( [14-Mar-2006] $)
  $}

  ${
    $d x A $.  $d x B $.
    $( Union of a singleton in the form of a restricted class abstraction. $)
    unisn3 $p |- ( A e. B -> U. { x e. B | x = A } = A ) $=
      ( wcel cv wceq crab cuni csn rabsn unieqd unisng eqtrd ) BCDZAEBFACGZHBIZ
      HBNOPACBJKBCLM $.
      $( [3-Jul-2008] $)
  $}

  ${
    $d x y z $.
    $( The class of all singletons is a proper class.  (Proof shortened by Eric
       Schmidt, 7-Dec-2008.) $)
    snnex $p |- { x | E. y x = { y } } e/ _V $=
      ( vz cv csn wceq wex cab cvv wnel wcel wn cuni vprc vex snid isseti mpbir
      wa sneq eqcoms eximi ax-mp snex eleq2 eqeq1 exbidv anbi12d cla4ev eluniab
      mp2an 2th eqriv eleq1i mtbir uniexg mto df-nel ) ADZBDZEZFZBGZAHZIJVDIKZL
      VEVDMZIKZVGIIKNVFIICVFICDZVFKZVHIKVIVHUSKZVCSZAGZVHVHEZKZVMVAFZBGZVLVHCOZ
      PUTVHFZBGVPBVHVQQVRVOBVOVHUTVHUTTUAUBUCVKVNVPSAVMVHUDUSVMFZVJVNVCVPUSVMVH
      UEVSVBVOBUSVMVAUFUGUHUIUKVCAVHUJRVQULUMUNUOVDIUPUQVDIURR $.
      $( [10-Oct-2008] $)
  $}

  $( If the subtrahend of a class difference exists, then the minuend exists
     iff the difference exists.  (The proof was shortened by Andrew Salmon,
     12-Aug-2011.) $)
  difex2 $p |- ( B e. C -> ( A e. _V <-> ( A \ B ) e. _V ) ) $=
    ( wcel cvv cdif difexg wa cun ssun2 uncom undif2 eqtr2i sseqtri unexg ssexg
    wss sylancr expcom impbid2 ) BCDZAEDZABFZEDZABEGUDUAUBUDUAHAUCBIZQUEEDUBABA
    IZUEABJUEBUCIUFUCBKBALMNUCBECOAUEEPRST $.
    $( [12-Aug-2011] $) $( [12-Nov-2003] $)

  $( Each member of an ordered pair belongs to the union of the union of a
     class to which the ordered pair belongs.  Lemma 3D of [Enderton] p. 41. $)
  opeluu $p |- ( <. x , y >. e. A ->
               ( x e. U. U. A /\ y e. U. U. A ) ) $=
    ( cv cop wcel cuni cpr vex prid1 opi2 elunii mpan sylancr prid2 jca ) ADZBD
    ZEZCFZQCGZGZFZRUBFZTQQRHZFUEUAFZUCQRAIJUESFTUFQRKUESCLMZQUEUALNTRUEFUFUDQRB
    IOUGRUEUALNP $.
    $( [31-Mar-1995] $)

  ${
    $d A x y v z $.  $d A x y u z $.
    $( Expression for double union that moves union into a class builder.
       (Contributed by FL, 28-May-2007.) $)
    uniuni $p |- U. U. A = U. { x | E. y ( x = U. y /\ y e. A ) } $=
      ( vz vu vv cv wcel cuni wa wex cab eluni anbi2i exbii 19.42v bicomi bitri
      wceq 3bitri excom anass ancom bitr3i 2exbii exdistr uniex ceqsexv exancom
      vex eleq2 3bitr2i eqeq1 anbi1d exbidv elab abbii df-uni 3eqtr4i ) DGZEGZH
      ZVACIZHZJZEKZDLUTFGZHZVGAGZBGZIZSZVJCHZJZBKZALZHZJZFKZDLVCIVPIVFVSDVFVBVA
      VJHZVMJZBKZJZEKZVMUTVKHZJZBKZVSVEWCEVDWBVBBVACMNOWDVMVBVTJZEKZJZBKZWGWDVB
      WAJZBKZEKZVMWHJZEKBKZWKWCWMEWMWCVBWABPQOWNWLEKBKWPWLEBUAWLWOBEWLWHVMJWOVB
      VTVMUBWHVMUCUDUERVMWHBEUFTWJWFBWIWEVMWEWIEUTVJMQNORWGVHVGVKSZVMJZJZBKFKZV
      HWRBKZJZFKVSWGWSFKZBKWTWFXCBWFVMVHWQJZFKZJVMXDJZFKXCWEXEVMWEWQVHJFKXEVHWE
      FVKVJBUJUGVGVKUTUKUHWQVHFUIUDNVMXDFPXFWSFXFXDVMJWSVMXDUCVHWQVMUBROULOWSBF
      UARVHWRFBUFXBVRFXAVQVHVQXAVOXAAVGFUJVIVGSZVNWRBXGVLWQVMVIVGVKUMUNUOUPQNOT
      TUQDEVCURDFVPURUS $.
      $( [5-Feb-2008] $) $( [28-May-2007] $)
  $}

  ${
    $d x y z $.  $d y z ph $.
    $( If ` ph ` is true for exactly one ` x ` , then ` U. { x | ph } ` is a
       way to express "the unique element such that ` ph ` is true."  Some
       books use a special symbol such as iota to denote "the unique element
       such that." $)
    euuni $p |- ( E! x ph -> ( ph <-> U. { x | ph } = x ) ) $=
      ( vy vz weu cab cuni cv wceq wa wex cvv wcel euabex uniexg syl eueq ax-17
      wb sylib eqcom eubii hbab1 hbuni hbeq eqeq2 cbveu 3bitri csn euabsn eleq2
      vex snid mpbiri abid unieq unisn syl6eq jca eximi sylbi eupickb mpd3an23
      ) ABEZABFZGZBHZIZBEZAVHJZBKZAVHSVDVFLMZVIVDVELMVLABNVELOPVLCHZVFIZCEVFVMI
      ZCEVICVFQVNVOCVMVFUAUBVOVHCBBDDVFVMBDVEABDUCUDDHVMMBRUEVHCRVMVGVFUFUGUHTV
      DVEVGUIZIZBKVKABUJVQVJBVQAVHVQVGVEMZAVQVRVGVPMVGBULZUMVEVPVGUKUNABUOTVQVF
      VPGVGVEVPUPVGVSUQURUSUTVAAVHBVBVC $.
      $( [22-Feb-2004] $)
  $}

  ${
    $d x y $.  $d y ph $.
    $( A way to express "the unique element such that" (restricted quantifier
       version).  (The proof was shortened by Andrew Salmon, 12-Aug-2011.) $)
    reuuni1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
            ( ph <-> U. { x e. A | ph } = x ) ) $=
      ( wreu cv wcel crab cuni wceq wb cab weu df-reu euuni sylbi df-rab unieqi
      wa ibar eqeq1i a1i bibi12d syl5ibrcom impcom ) ABCDZBEZCFZAABCGZHZUFIZJZU
      EUKUGUGARZULBKZHZUFIZJZUEULBLUPABCMULBNOUGAULUJUOUGASUJUOJUGUIUNUFUHUMABC
      PQTUAUBUCUD $.
      $( [12-Aug-2011] $) $( [25-Nov-1994] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    reuuni2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    reuuni2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    reuuni2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ."  This theorem shows a condition
       that allows us to represent this element with a class expression
       ` B ` .  The second hypothesis is a weakened bound variable condition
       that allows ~ hbsbc1g to be used. $)
    reuuni2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( wcel wreu crab cuni wceq wb wi cv ax-17 wal a1i hbel hbreu1 hbrab1 hbeq
      hbuni hbbid hbimd hbim1 bibi12d imbi2d imbi12d reuuni1 ex vtoclgf pm2.43i
      eleq1 eqeq2 imp ) FEJZACEKZBACELZMZFNZOZUSUTVDPZCQZEJZUTAVBVFNZOZPZPUSVEP
      CDFEGUSVECCDDFEGDQEJCRUAZUSUTVDCVKUTUTCSPUSACEUBTUSBVCCVKHVCVCCSPUSCDDVBF
      CDVAACDEUCUEGUDTUFUGUHVFFNZVGUSVJVEVFFEUPVLVIVDUTVLABVHVCIVFFVBUQUIUJUKVG
      UTVIACEULUMUNUOUR $.
      $( [19-Oct-2005] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reuuni2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( ` U. { x e. A | ph } ` is an explicit representation of "the unique
       element in ` A ` such that ` ph ` ." $)
    reuuni2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> U. { x e. A | ph } = B ) ) $=
      ( vy cv wcel ax-17 a17d reuuni2f ) ABCGDEGHEICJEDIBCKFL $.
      $( [27-May-2006] $) $( [28-Mar-1997] $)
  $}

  ${
    $d A x $.  $d ps x $.
    euuni2.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( The unique element such that ` ph ` .  (Contributed by Jeff Madsen,
       1-Jun-2011.) $)
    euuni2 $p |- ( ( A e. B /\ E! x ph ) -> ( ps <-> U. { x | ph } = A ) ) $=
      ( wcel cvv wreu cab cuni wceq wb weu elex reuv biimpri wa crab reuuni2
      rabab unieqi eqeq1i syl6bb syl2an ) DEGDHGZACHIZBACJZKZDLZMACNZDEOUGUKACP
      QUFUGRBACHSZKZDLUJABCHDFTUMUIDULUHACUAUBUCUDUE $.
      $( [1-Jun-2011] $)
  $}

  ${
    $d x y A $.  $d y ph $.  $d x ps $.  $d x ch $.
    reuuni3.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    reuuni3.2 $e |- ( x = U. { y e. A | ps } -> ( ph <-> ch ) ) $.
    $( Derive the property ` ch ` of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { y e. A | ps } ` . $)
    reuuni3 $p |- ( E! x e. A ph -> ch ) $=
      ( crab cuni wcel wreu cbvrabv unieqi reucl syl5eqelr wceq reuuni2 mpbiri
      wa mpancom ) BEFIZJZFKZADFLZCUEUCADFIZJZFUFUBABDEFGMNZADFOPUDUETCUGUCQUHA
      CDFUCHRSUA $.
      $( [11-Nov-2004] $)
  $}

  ${
    $d x y A $.  $d y ph $.
    $( Derive the property of "the unique element in ` A ` such that
       ` ph ` " when expressed explicitly as ` U. { x e. A | ph } ` . $)
    reuunisbc $p |- ( E! x e. A ph -> [ U. { x e. A | ph } / x ] ph ) $=
      ( vy wreu crab cuni wcel wsbc reucl cvv elex wrex wi reurex hbreu1 hbrab1
      hbuni hbsbc1 mpd cv wa wceq wb reuuni1 sbceq1a eqcoms syl6bi expcom com4l
      ibd a1i rexlimd syl5 ) ABCEZABCFZGZCHZABUQIZABCJURUQKHZUOUSUQCLUOABCMUTUS
      NZABCOUOAVABCABCPABDUQKBDUPABDCQRSUTUOBUAZCHZAUSUOVCAUSNZNNUTVCUOVDVCUOUB
      ZAUSVEAUQVBUCAUSUDZABCUEVFVBUQABUQUFUGUHUKUIULUJUMTUNT $.
      $( [11-Nov-2004] $)

    $( Membership law for "the unique element in ` A ` such that ` ph ` ." $)
    reucl2 $p |- ( E! x e. A ph -> U. { x e. A | ph } e. { x e. A | ph } ) $=
      ( vy wreu crab cuni wcel wsbc reucl reuunisbc cv ax-17 elrabsf sylanbrc )
      ABCEABCFZGZCHABQIQPHABCJABCKABDQCDLCHBMNO $.
      $( [11-Jun-2005] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss $p |- ( ( A C_ B /\ E. x e. A ph /\ E! x e. B ph ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( vy wss wrex wreu w3a crab cuni wsbc wceq reuss reuunisbc syl wcel reucl
      wb wi ssel 3ad2ant1 mpd simp3 hbrab1 hbuni hbsbc1g sbceq1a reuuni2f mpbid
      syl2anc eqcomd ) CDFZABCGZABDHZIZABDJKZABCJZKZUPABUSLZUQUSMZUPABCHZUTABCD
      NZABCOPUPUSDQZUOUTVASUPUSCQZVDUPVBVEVCABCRPUMUNVEVDTUOCDUSUAUBUCUMUNUOUDA
      UTBEDUSBEURABECUEUFZABEUSDVFUGABUSUHUIUKUJUL $.
      $( [19-Oct-2005] $)

    $( Restriction of a unique element to a smaller class. $)
    mouniss $p |- ( ( A C_ B /\ E. x e. A ph /\ E* x ( x e. B /\ ph ) ) ->
                U. { x e. A | ph } = U. { x e. B | ph } ) $=
      ( wss wrex cv wcel wa wmo crab cuni wceq wreu ssel anim1d reximdv2 anim1i
      imp reu5 sylibr reuuniss 3expa syldan 3impa ) CDEZABCFZBGZDHZAIBJZABCKLAB
      DKLMZUFUGIZUJABDNZUKULUJIABDFZUJIUMULUNUJUFUGUNUFAABCDUFUHCHUIACDUHOPQSRA
      BDTUAUFUGUMUKABCDUBUCUDUE $.
      $( [19-Feb-2006] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class. $)
    reuuniss2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) ) -> U. { x e. A | ph } = U. { x e. B | ps } ) $=
      ( vy wss wi wral wa wrex wreu crab cuni wsbc wceq reuss2 simplr wcel syl
      reuunisbc reucl ra4sbc sbcimg sylibd mpid sylc ad2antrr mpd simprr hbrab1
      wb ssel hbuni hbsbc1g sbceq1a reuuni2f syl2anc mpbid eqcomd ) DEGZABHZCDI
      ZJZACDKZBCELZJZJZBCEMNZACDMZNZVHBCVKOZVIVKPZVHACDLZVCVLABCDEQZVAVCVGRVNVC
      ACVKOZVLACDUAVNVKDSZVCVPVLHZHACDUBZVQVCVBCVKOVRVBCVKDUCABCVKDUDUETUFUGVHV
      KESZVFVLVMULVHVQVTVHVNVQVOVSTVAVQVTHVCVGDEVKUMUHUIVDVEVFUJBVLCFEVKCFVJACF
      DUKUNZBCFVKEWAUOBCVKUPUQURUSUT $.
      $( [20-Oct-2005] $)
  $}

  ${
    $d y z A $.  $d y z ph $.  $d x y z $.
    $( A way to express restricted existential uniqueness of a wff: its
       restricted class abstraction is a singleton.  (The proof was shortened
       by Andrew Salmon, 12-Aug-2011.) $)
    reusn $p |- ( E! x e. A ph <-> E. y { x e. A | ph } = { y } ) $=
      ( vz wreu cv wcel wa weu cab csn wceq wex crab df-reu euabsn ax-17 hbrab1
      hbeq df-rab eqcomi a1i sneq eqeq12d cbvex 3bitri ) ABDFBGZDHAIZBJUIBKZUHL
      ZMZBNABDOZCGZLZMZCNABDPUIBQULUPBCULCRBEEUMUOABEDSEGUOHBRTUHUNMZUJUMUKUOUJ
      UMMUQUMUJABDUAUBUCUHUNUDUEUFUG $.
      $( [12-Aug-2011] $) $( [30-May-2006] $)
  $}

  ${
    $d y A $.  $d y B $.  $d y ph $.  $d x y $.
    reusni.1 $e |- B e. _V $.
    $( Restricted existential uniqueness determined by a singleton. $)
    reusni $p |- ( { x e. A | ph } = { B } -> E! x e. A ph ) $=
      ( vy crab csn wceq cv wex wreu sneq eqeq2d cla4ev reusn sylibr ) ABCGZDHZ
      IZRFJZHZIZFKABCLUCTFDEUADIUBSRUADMNOABFCPQ $.
      $( [29-May-2006] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x ps $.
    rabsnt.1 $e |- B e. _V $.
    rabsnt.2 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( Truth implied by equality of a restricted class abstraction and a
       singleton. $)
    rabsnt $p |- ( { x e. A | ph } = { B } -> ps ) $=
      ( crab csn wceq wcel wreu wa cuni wss ssrab2 sseq1 mpbii snss sylibr sylc
      reusni jca unieq unisn syl6eq reuuni2 biimprd ) ACDHZEIZJZEDKZACDLZMZUINZ
      EJZBUKULUMUKUJDOZULUKUIDOUQACDPUIUJDQREDFSTACDEFUBUCUKUOUJNEUIUJUDEFUEUFU
      NBUPABCDEGUGUHUA $.
      $( [29-May-2006] $)
  $}

  ${
    $d y A $.  $d y ph $.  $d x y $.
    $( A restricted class abstraction with a unique member can be expressed as
       a singleton. $)
    reuunisn $p |- ( E! x e. A ph ->
                   { x e. A | ph } = { U. { x e. A | ph } } ) $=
      ( vy wreu crab csn wceq wex cuni reusn unieq vex unisn syl6eq sneqd eqtr3
      cv mpdan exlimiv sylbi ) ABCEABCFZDRZGZHZDIUBUBJZGZHZABDCKUEUHDUEUGUDHUHU
      EUFUCUEUFUDJUCUBUDLUCDMNOPUBUGUDQSTUA $.
      $( [30-May-2006] $)
  $}

  ${
    $d x y z $.  $d A y z $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    eusv1 $p |- ( E! y A. x y = A <-> E. y A. x y = A ) $=
      ( vz cv wceq wal weu wex wa wi eqeq1 albidv eu4 ax-4 syl2an gen2 mpbiran2
      eqtr3 ) BEZCFZAGZBHUBBIUBDEZCFZAGZJTUCFZKZDGBGUBUEBDUFUAUDATUCCLMNUGBDUBU
      AUDUFUEUAAOUDAOTUCCSPQR $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y z w $.  $d A y z w $.
    $( Even if ` x ` is free in ` A ` , it is effectively bound when
       ` A ( x ) ` is single-valued. $)
    eusvhb $p |- ( E! y A. x y = A -> ( y = A -> A. x y = A ) ) $=
      ( vz vw cv wceq wal weu wreu wi wcel wa biantrur eubii df-reu eqeq1 ax-17
      cvv vex bitr4i crab cuni albidv hba1 hbrab hbuni hbeq albid reuuni3 eqtr3
      19.21bi syl5ibrcom syl5 mpan2d sylbi ) BFZCGZAHZBIZUSBSJZURUSKUTUQSLZUSMZ
      BIVAUSVCBVBUSBTNOUSBSPUAVAURDFZCGZAHZDSUBZUCZCGZUSVAVIAUSVFVIAHZBDSUQVDGU
      RVEAUQVDCQUDUQVHGZURVIAAEEUQVHEFZUQLARAEVGVFADESVEAUEVLSLARUFUGUHUQVHCQUI
      ZUJZULURVIMVKVAUSUQVHCUKVAUSVKVJVNVMUMUNUOUP $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y $.  $d A y $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    eusv2i $p |- ( E! y A. x y = A -> E! y E. x y = A ) $=
      ( wceq wal weu wex hbeu1 hba1 hbeu eusvhb exlimd 19.2 impbid1 eubid ibir
      cv ) BQCDZAEZBFZRAGZBFTUASBSBHTUASTRSASABRAIZJUBABCKLRAAMNOP $.
      $( [14-Oct-2010] $)
  $}

  ${
    $d x y z v w $.  $d A y z w v $.
    eusv2.1 $e |- A e. _V $.
    $( Two ways to express single-valuedness of a class expression
       ` A ( x ) ` . $)
    eusv2 $p |- ( E! y E. x y = A <-> E! y A. x y = A ) $=
      ( vz vw vv cv wceq wex weu wal cvv wcel wreu wa ax-17 eqeq1 sylbi wi crab
      cuni vex biantrur eubii exbidv cbveu df-reu 3bitr4i reucl hbe1 hbeu hbrab
      eqid eu4 hbuni exbid anbi1d imbi12d albidv cla4gv com12 adantl mpd bitr4i
      hbeq reuuni3 biantrurd imbi1d mpbird 19.23 albii alcom bitr3i sylib eqeq2
      ax-4 cla4v 3syl mpi alrimi albid cla4egv sylc eusv1 sylibr eusv2i impbii
      ) BHZCIZAJZBKZWJALZBKZWLWMBJZWNWLEHZCIZAJZEMUAZUBZMNZWTCIZALZWOWLWREMOZXA
      WREKWPMNZWRPZEKWLXDWRXFEXEWREUCUDUEWKWRBEWKEQWRBQWIWPIWJWQAWIWPCRUFZUGWRE
      MUHUIWREMUJSZWLXBAWKABWJAUKULWLCCIZXBCUNWLFHZCIZWTXJIZTZFLZALZXNXIXBTZWLX
      KAJZXLTZFLZXOWLXSXBAJZXQPZXLTZFLZWLXAYCXHWLWKBJZWKXQPZWIXJIZTZFLZBLZPXAYC
      TZWKXQBFYFWJXKAWIXJCRUFUOYIYJYDXAYIYCYHYCBWTMWIWTIZYGYBFYKYEYAYFXLYKWKXTX
      QYKWJXBAAGGWIWTGHZWINAQAGWSWRAEGMWQAUKYLMNAQUMUPZVFZWIWTCRZUQZURWIWTXJRUS
      UTVAVBVCSVDWLXRYBFWLXQYAXLWLXTXQWLWKBMOZXTWLWIMNZWKPZBKYQWKYSBYRWKBUCUDUE
      WKBMUHVEWKWRXTBEMXGYPVGSVHVIUTVJXSXMALZFLXOYTXRFXKXLAAGGWTXJYMYLXJNAQVFVK
      VLXMFAVMVNVOXNAVQXMXPFCDXKXKXIXLXBXJCCRXJCWTVPUSVRVSVTWAWMXCBWTMYKWJXBAYN
      YOWBWCWDABCWEWFABCWGWH $.
      $( [15-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x z C $.
    $( Lemma for ~ reusv1 . $)
    reusv1lem $p |- ( B =/= (/) -> ( E! x e. A A. y e. B x = C
                     <-> E. x e. A A. y e. B x = C ) ) $=
      ( vz c0 wne cv wcel wex wceq wral wreu wrex wb n0 ax-17 hbra1 wa hbreu wi
      hbrex hbbi ra4 eqtr3 syl6com syl5bir a1d ralrimivv biantrud eqeq1 ralbidv
      r19.26 reu4 syl6rbbr exlimi sylbi ) DGHBIDJZBKAIZELZBDMZACNZVBACOZPZBDQUS
      VEBVCVDBVBBACUTCJZBRZVABDSZUAVBBACVGVHUCUDUSVDVDVBFIZELZBDMZTZUTVILZUBZFC
      MACMZTVCUSVOVDUSVNAFCCUSVNVFVICJTVLVAVJTZBDMZUSVMVAVJBDUNVQUSVPVMVPBDUEUT
      VIEUFUGUHUIUJUKVBVKAFCVMVAVJBDUTVIEULUMUOUPUQUR $.
      $( [22-Oct-2010] $)
  $}

  ${
    $d x z A $.  $d w x z B $.  $d x z C $.  $d w x z ph $.  $d w x y z $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` . $)
    reusv1 $p |- ( E. y e. B ph
       -> ( E! x e. A A. y e. B ( ph -> x = C )
                   <-> E. x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw wrex cv wcel c0 wne wceq wi wral wreu ax-17 reubii rexbii wa crab
      cab wb rabn0 df-rab neeq1i bitr3i csb reusv1lem vex hbcsb1 hbeleq csbeq1a
      hbab1 eqeq2d cbvralf 3bitr4g abid imbi1i impexp bitr2i ralbii2 sylbi ) AC
      EIZCJZEKZAUAZCUCZLMZABJZFNZOZCEPZBDQZVNBDIZUDVEACEUBZLMVJACEUEVQVILACEUFU
      GUHVJVLCVIPZBDQZVRBDIZVOVPVJVKCGJZFUIZNZGVIPZBDQWDBDIVSVTBGDVIWBUJVRWDBDV
      LWCCGHVIVHCHUOHJVIKGRVLGRCBWBCBWAFGUKVKWAKCRULUMVFWANFWBVKCWAFUNUPUQZSVRW
      DBDWETURVNVRBDVMVLCEVIVFVIKZVLOVHVLOVGVMOWFVHVLVHCUSUTVGAVLVAVBVCZSVNVRBD
      WGTURVD $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Lemma for ~ reusv2 . $)
    reusv2lem1 $p |- ( A =/= (/) -> ( E! x A. y e. A x = B
                     <-> E. x A. y e. A x = B ) ) $=
      ( vz c0 wne cv wcel wex wceq wral weu wb n0 hbra1 hbeu hbex wa wal r19.26
      wi ra4 eqtr3 syl6com syl5bir alrimivv biantrud eqeq1 ralbidv eu4 syl6rbbr
      hbbi exlimi sylbi ) CFGBHCIZBJAHZDKZBCLZAMZUSAJZNZBCOUPVBBUTVABUSBAURBCPZ
      QUSBAVCRUMUPVAVAUSEHZDKZBCLZSZUQVDKZUBZETATZSUTUPVJVAUPVIAEVGURVESZBCLZUP
      VHURVEBCUAVLUPVKVHVKBCUCUQVDDUDUEUFUGUHUSVFAEVHURVEBCUQVDDUIUJUKULUNUO $.
      $( [22-Oct-2010] $)
  $}

  ${
    $d v w x y z A $.  $d v w x z B $.
    $( Lemma for ~ reusv2 . $)
    reusv2lem2 $p |- ( E! x A. y e. A x = B -> E! x E. y e. A x = B ) $=
      ( vz vw vv cv wceq wral weu wrex wi c0 wn wal wa ax-17 wcel cvv wex eunex
      exnal sylib rzal alrimiv nsyl3 pm2.21 syl wne adantl r19.2z eximdv adantr
      euex ex mpd hbeu1 csb vex hbcsb1 hbeleq reean eqeq2d cbvrex anbi2i bitr4i
      csbeq1a crab cuni eqeq1 ralbidv hbra1 hbrab hbuni ralbid reuuni3 biantrur
      wreu hbeq eubii df-reu anidm 3imtr4i raaan cbvral eqtr2 eqeq12 syl5ibrcom
      ralimi sylbir r19.23v ralbii bitri syl5bir rexbidv eu4 sylanbrc pm2.61ine
      alrimi ) AHZDIZBCJZAKZXBBCLZAKZMZCNCNIZXDOXGXDXCAPZXHXDXCOAUAXIOXCAUBXCAU
      CUDXHXCAXBBCUEUFUGXDXFUHUICNUJZXDXFXJXDQZXEAUAZXEEHZDIZBCLZQZXAXMIZMZEPZA
      PZXFXKXCAUAZXLXDYAXJXCAUOUKXJYAXLMXDXJXCXEAXJXCXEXBBCULUPUMUNUQXDXTXJXDXS
      AXCAURXDXREXPXBXMBFHZDUSZIZQZFCLZBCLZXDXQYGXEYDFCLZQXPXBYDBFCCXBFRBEYCBEY
      BDFUTZXMYBSBRVAVBZVCXOYHXEXNYDBFCXNFRYJBHYBIZDYCXMBYBDVHZVDVEVFVGXDYEXQMZ
      FCJZBCJZYGXQMZXDXNBCJZETVIZVJZDIZBCJZUUAQZYOXCATVSZUUAXDUUBXCYQUUAAETXQXB
      XNBCXAXMDVKZVLXAYSIXBYTBCBGGXAYSGHZXASBRBGYRYQBEGTXNBCVMUUETSBRVNVOZVTXAY
      SDVKVPVQXDXATSZXCQZAKUUCXCUUHAUUGXCAUTVRWAXCATWBVGUUAWCWDUUBYTYSYCIZQZFCJ
      ZBCJZYOUULUUAUUIFCJZQUUBYTUUIBFCYTFRZBGGYSYCUUFBGYBDYIUUEYBSBRVAVTZWEUUAU
      UMUUAYTUUIBFCUUNUUOYKDYCYSYLVDWFVFVGUUKYNBCUUJYMFCUUJXQYEDYCIYSDYCWGXADXM
      YCWHWIWJWJWKUIYOYFXQMZBCJYPYNUUPBCYEXQFCWLWMYFXQBCWLWNUDWOUFWTUKXEXOAEXQX
      BXNBCUUDWPWQWRUPWS $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    $( Lemma for ~ reusv2 . $)
    reusv2lem3 $p |- ( A. y e. A B e. _V ->
          ( E! x E. y e. A x = B <-> E! x A. y e. A x = B ) ) $=
      ( vz cvv wcel wral cv wceq wrex weu wi c0 wex rex0 rexeq wa wal eqeq1 a1i
      mtbiri nexdv euex pm2.21d wne weq ax-17 eu3 ralcom4 r19.23v albii imbi12d
      nsyl bitri eqid eqcom bitr3i syl6bb cla4gv ral2imi syl5bir eximdv ralbidv
      a1bi cbvexv syl6ibr adantld syl5bi adantr wb reusv2lem1 adantl sylibrd ex
      pm2.61dne reusv2lem2 impbid1 ) DFGZBCHZAIZDJZBCKZALZWBBCHZALZVTWDWFMZCNCN
      JZWGMVTWHWDWFWHWCAOZWDWHWCAWHWCWBBNKWBBPWBBCNQUBUCWCAUDUNUEUAVTCNUFZWGVTW
      JRWDWEAOZWFVTWDWKMWJWDWIWCAEUGZMZASZEOZRVTWKWCAEWCEUHUIVTWOWKWIVTWOEIZDJZ
      BCHZEOWKVTWNWREWNWBWLMZASZBCHZVTWRXAWSBCHZASWNWSBACUJXBWMAWBWLBCUKULUOVSW
      TWQBCWSWQADFWBWSDDJZDWPJZMZWQWBWBXCWLXDWADDTWADWPTUMXEXDWQXCXDDUPVEDWPUQU
      RUSUTVAVBVCWEWRAEWLWBWQBCWAWPDTVDVFVGVHVIVJWJWFWKVKVTABCDVLVMVNVOVPABCDVQ
      VR $.
      $( [14-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.  $d x z C $.  $d x z ph $.
    $( Lemma for ~ reusv2 . $)
    reusv2lem4 $p |- ( E! x e. A E. y e. B ( ph /\ x = C )
            <-> E! x A. y e. B ( ( C e. A /\ ph ) -> x = C ) ) $=
      ( vz cv wceq wa wrex wcel weu cab wi wex anass ax-17 wal bitri csb df-reu
      wreu wral r19.42v df-rex abid anbi1i eleq1 anbi2d anbi1d pm5.32ri 3bitrri
      bitr3i exbii hbab1 hbcsb1 hbeleq hban csbeq1a eqeq2d anbi12d cbvex bitr4i
      vex 3bitri eubii wb reusv2lem3 simplr ss2abi sseli hbel eleq1d elabf elex
      cvv sylbi syl mprg df-ral hbim imbi12d cbval imbi1i impexp albii 3bitr2i
      ) ABHZFIZJZCEKZBDUCWIDLZWLJZBMWICGHZFUAZIZGCHZELZFDLZJZAJZCNZKZBMZWTAJZWJ
      OZCEUDZBMZWLBDUBWNXDBWNWMWKJZCEKZXDWMWKCEUEXKWSXJJZCPWRXCLZWJJZCPZXDXJCEU
      FXLXNCXNXBWJJZWSWMJZWKJZXLXMXBWJXBCUGZUHXPXQAJZWJJXRWJXTXBWJXQXAAWJWMWTWS
      WIFDUIUJUKULXQAWJQUNWSWMWKQUMUOXOWOXCLZWQJZGPXDXNYBCGXNGRYAWQCXBCGUPZCBWP
      CBWOFGVEZWIWOLCRUQZURZUSWRWOIZXMYAWJWQWRWOXCUIZYGFWPWICWOFUTZVAZVBVCWQGXC
      UFVDVFUNVGXEWQGXCUDZBMZXIWPVQLZXEYLVHGXCBGXCWPVIYAWOWTCNZLZYMXCYNWOXBWTCW
      SWTAVJVKVLYOWPDLZYMWTYPCWOCBGWPDYEWODLCRVMYDYGFWPDYIVNVOWPDVPVRVSVTYKXHBY
      KYAWQOZGSXMWJOZCSZXHWQGXCWAYRYQCGYRGRYAWQCYCYFWBYGXMYAWJWQYHYJWCWDYSWSXGO
      ZCSXHYRYTCYRWSXFJZWJOYTXMUUAWJXMXBUUAXSWSWTAQTWEWSXFWJWFTWGXGCEWAVDWHVGTV
      F $.
      $( [13-Dec-2012] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.
    $( Lemma for ~ reusv2 . $)
    reusv2lem5 $p |- ( ( A. y e. B C e. A /\ B =/= (/) )
          -> ( E! x e. A E. y e. B x = C <-> E! x e. A A. y e. B x = C ) ) $=
      ( wcel wral c0 wne wa cv wceq wrex wreu weu wi wb bitrd eubidv syl5bb ra4
      equid biantrur rexbii reubii reusv2lem4 hbra1 id imp eleq1 pm5.32ri biimt
      baib biantru imbi1i syl6rbb ralbida adantr r19.28zv adantl df-reu syl6bbr
      syl sylan ) ECFZBDGZDHIZJZAKZELZBDMZACNZVICFZVJBDGZJZAOZVNACNVHVLVMVJJZBD
      GZAOZVPVLBKZVTLZVJJZBDMZACNZVHVSVKWCACVJWBBDWAVJBUBZUCUDUEWDVEWAJZVJPZBDG
      ZAOZVHVSWAABCDEUFVFWIVSQVGVFWHVRAVFWGVQBDVEBDUGVFVTDFZVEPZWJWGVQQZVEBDUAW
      KWJJVEWLWKWJVEWKUHUIVEVQVEVJPZWGVEVQVJWMVQVEVJVJVMVEVIECUJUKUMVEVJULRVEWF
      VJWAVEWEUNUOUPVCVDUQSURTTVGVSVPQVFVGVRVOAVMVJBDUSSUTRVNACVAVB $.
      $( [4-Jan-2013] $)
  $}

  ${
    $d w x y z A $.  $d w x z B $.  $d w x z C $.  $d w x z ph $.
    $( Two ways to express single-valuedness of a class expression ` C ( y ) `
       that is constant for those ` y e. B ` such that ` ph ` .  The first
       antecedent ensures that the constant value belongs to the existential
       uniqueness domain ` A ` , and the second ensures that ` C ( y ) ` is
       evaluated for at least one ` y ` . $)
    reusv2 $p |- ( ( A. y e. B ( ph -> C e. A ) /\ E. y e. B ph )
          -> ( E! x e. A E. y e. B ( ph /\ x = C )
                   <-> E! x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw wcel wi wral cv wa c0 wrex wreu wal ax-17 wsb reubii csb cab wceq
      wne wb df-ral hbab1 vex wel hbcsb1 hbel hbim weq sbequ12 sbim df-clab cvv
      sbcel1g ax-mp bicomi bitr4i syl6bb cbval impexp albii 3bitr2ri bitri crab
      imbi12i rabn0 df-rab neeq1i bitr3i hbeleq csbeq1a cbvrexf cbvralf 3bitr4g
      reusv2lem5 eqeq2d abid anbi1i anass bitr2i rexbii2 imbi1i ralbii2 syl2anb
      ) AFDIZJZCEKZCGLZFUAZDIZGCLZEIZAMZCUBZKZWRNUDZABLZFUCZMZCEOZBDPZAXBJZCEKZ
      BDPZUEACEOZWKWPWJJZCQZWSWJCEUFWSWLWRIZWNJZGQWQWIJZCQXKWNGWRUFXNXMCGXNGRXL
      WNCWQCGUGCHHWMDCHWLFGUHZHGUICRUJHLZDICRUKULCGUMZXNXNCGSZXMXNCGUNXRWQCGSZW
      ICGSZJXMWQWICGUOXLXSWNXTWQGCUPXTWNWLUQIXTWNUEXOCWLFDUQURUSUTVIVAVBVCXNXJC
      WPAWIVDVEVFVGXIACEVHZNUDWTACEVJYAWRNACEVKVLVMWSWTMZXBCWROZBDPZXBCWRKZBDPZ
      XEXHYBXAWMUCZGWROZBDPYGGWRKZBDPYDYFBGDWRWMVSYCYHBDXBYGCGHWRWQCHUGZXPWRIGR
      ZXBGRZCBWMCBWLFXOBGUICRUJVNZXQFWMXACWLFVOVTZVPTYEYIBDXBYGCGHWRYJYKYLYMYNV
      QTVRXDYCBDXCXBCEWRWOWRIZXBMWQXBMWPXCMYOWQXBWQCWAZWBWPAXBWCWDWETXGYEBDXFXB
      CEWRYOXBJWQXBJWPXFJYOWQXBYPWFWPAXBVDWDWGTVRWH $.
      $( [4-Jan-2013] $)
  $}

  ${
    $d x y z B $.  $d x z C $.  $d x y D $.  $d x z ph $.  $d x y ps $.
    reusv3.1 $e |- ( y = z -> ( ph <-> ps ) ) $.
    reusv3.2 $e |- ( y = z -> C = D ) $.
    $( Two ways of expressing existential uniqueness via an indirect
       equality. $)
    reusv3i $p |- ( E. x e. A A. y e. B ( ph -> x = C )
            -> A. y e. B A. z e. B ( ( ph /\ ps ) -> C = D ) ) $=
      ( cv wceq wi wral wa eqeq2d imbi12d cbvralv ralimi biimpi prth eqtr2 syl6
      raaanv sylbir mpdan rexlimivw ) ACLZHMZNZDGOZABPZHIMZNZEGOZDGOZCFULBUIIMZ
      NZEGOZUQULUTUKUSDEGDLELMZABUJURJVAHIUIKQRSUAULUTPUKUSPZEGOZDGOUQUKUSDEGUE
      VCUPDGVBUOEGVBUMUJURPUNAUJBURUBUIHIUCUDTTUFUGUH $.
      $( [23-Dec-2012] $)

    $d x y z A $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` .  See ~ reusv1 for the connection to uniqueness. $)
    reusv3 $p |- ( E. y e. B ( ph /\ C e. A )
          -> ( A. y e. B A. z e. B ( ( ph /\ ps ) -> C = D )
                   <-> E. x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( wcel wa wrex wceq wi wral cv ax-17 bitri eleq1d hbra1 hbral hbim risset
      anbi12d cbvrexv ralcom impexp bi2.04 ralbii r19.21v ra4 sylbi com3l imp31
      eqeq1 eqcom syl6bb imbi2d ralbidv syl5ibrcom reximdv com23 syl5bi expimpd
      ex rexlimi reusv3i impbid1 ) AHFLZMZDGNZABMHIOZPZEGQZDGQZACRZHOZPZDGQZCFN
      ZVMBIFLZMZEGNVQWBPZVLWDDEGDRZERZOZABVKWCJWHHIFKUAUFUGWDWEEGVQWBEVPEDGWFGL
      ESVOEGUBUCWBESUDWGGLZBWCWEWCVRIOZCFNZWIBMZWECIFUEWLVQWKWBWLVQWKWBPWLVQMZW
      JWACFWMWAWJAVNPZDGQZWIBVQWOVQWIBWOVQBWOPZEGQZWIWPPVQVODGQZEGQWQVODEGGUHWR
      WPEGWRBWNPZDGQWPVOWSDGVOABVNPPWSABVNUIABVNUJTUKBWNDGULTUKTWPEGUMUNUOUPWJV
      TWNDGWJVSVNAWJVSIHOVNVRIHUQIHURUSUTVAVBVCVGVDVEVFVHUNABCDEFGHIJKVIVJ $.
      $( [27-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    eusv4.1 $e |- B e. _V $.
    $( Two ways to express single-valuedness of a class expression
       ` B ( x ) ` . $)
    eusv4 $p |- ( E! x E. y e. A x = B <-> E! x A. y e. A x = B ) $=
      ( cvv wcel cv wceq wrex weu wral wb reusv2lem3 a1i mprg ) DFGZAHDIZBCJAKR
      BCLAKMBCABCDNQBHCGEOP $.
      $( [27-Oct-2010] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x C $.  $d x y $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` . $)
    reusv5OLD $p |- ( B =/= (/) -> ( E! x e. A A. y e. B x = C
                     <-> E. x e. A A. y e. B x = C ) ) $=
      ( c0 wne cv wceq wrex wral wreu wb wcel wex wa equid biantru exbii n0 wi
      df-rex 3bitr4i reusv1 a1bi ralbii reubii rexbii 3bitr4g sylbi ) DFGZBHZUL
      IZBDJZAHEIZBDKZACLZUPACJZMULDNZBOUSUMPZBOUKUNUSUTBUMUSBQZRSBDTUMBDUBUCUNU
      MUOUAZBDKZACLVCACJUQURUMABCDEUDUPVCACUOVBBDUMUOVAUEUFZUGUPVCACVDUHUIUJ $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d v w x y z A $.  $d v w x y z B $.  $d v w x z C $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` .  The converse does not hold.  Note that ` U. A = |^| A `
       means ` A ` is a singleton ( ~ uniintsn ). $)
    reusv6OLD $p |- ( ( U. A =/= |^| A \/ B =/= (/) )
          -> ( E! x e. A A. y e. B x = C -> E! x e. A E. y e. B x = C ) ) $=
      ( vz vw vv cv wceq wral wreu wrex wi c0 wcel wa bitr4i ax-17 a1d cuni wne
      cint wn raleq reubidv weu df-reu csn wex uniintsn eusn ral0 biantru eubii
      3bitr2i syl6bb necon3bbid pm2.21 syl6bir reurex adantl r19.2z reximdv mpd
      ex adantr hbreu1 csb vex hbcsb1 hbeleq reean csbeq1a eqeq2d cbvrex anbi2i
      crab eqeq1 ralbidv hbra1 hbrab hbuni hbeq ralbid anidm sylibr raaan eqtr2
      reuuni3 cbvral eqeq12 syl5ibrcom ralimi sylbir r19.23v ralbii bitri sylib
      syl syl5bir ralrimiv ralrimi rexbidv reu4 sylanbrc pm2.61ine jaoi ) CUAZC
      UCZUBZAIZEJZBDKZACLZXMBDMZACLZNZDOUBZXKXRNDODOJZXKXOUDXRXTXOXIXJXTXOXMBOK
      ZACLZXIXJJZXTXNYAACXMBDOUEUFYBXLCPZYAQZAUGZYCYAACUHYCCXLUIJAUJYDAUGYFACUK
      ACULYDYEAYAYDXMBUMUNUOUPRUQURXOXQUSUTXSXRXKXSXOXQXSXOQZXPACMZXPFIZEJZBDMZ
      QZXLYIJZNZFCKZACKZXQYGXNACMZYHXOYQXSXNACVAVBXSYQYHNXOXSXNXPACXSXNXPXMBDVC
      VFVDVGVEXOYPXSXOYOACXNACVHXOYOYDXOYNFCXOYNYICPYLXMYIBGIZEVIZJZQZGDMZBDMZX
      OYMUUCXPYTGDMZQYLXMYTBGDDXMGSBFYSBFYREGVJZYIYRPBSVKVLZVMYKUUDXPYJYTBGDYJG
      SUUFBIYRJZEYSYIBYREVNZVOVPVQRXOUUAYMNZGDKZBDKZUUCYMNZXOYJBDKZFCVRZUAZEJZB
      DKZUUQQZUUKXOUUQUURXNUUMUUQAFCYMXMYJBDXLYIEVSZVTXLUUOJXMUUPBDBHHXLUUOHIZX
      LPBSBHUUNUUMBFHCYJBDWAUUTCPBSWBWCZWDXLUUOEVSWEWJUUQWFWGUURUUPUUOYSJZQZGDK
      ZBDKZUUKUVEUUQUVBGDKZQUURUUPUVBBGDUUPGSZBHHUUOYSUVABHYREUUEUUTYRPBSVKWDZW
      HUUQUVFUUQUUPUVBBGDUVGUVHUUGEYSUUOUUHVOWKVQRUVDUUJBDUVCUUIGDUVCYMUUAEYSJU
      UOEYSWIXLEYIYSWLWMWNWNWOWTUUKUUBYMNZBDKUULUUJUVIBDUUAYMGDWPWQUUBYMBDWPWRW
      SXATXBTXCVBXPYKAFCYMXMYJBDUUSXDXEXFVFZTXGUVJXH $.
      $( [30-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x C $.
    reusv7.1 $e |- ( y e. B -> C e. A ) $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` .  Note that ` U. A = |^| A ` means ` A ` is a singleton
       ( ~ uniintsn ). $)
    reusv7OLD $p |- ( ( U. A =/= |^| A \/ B =/= (/) )
          -> ( E! x e. A E. y e. B x = C <-> E! x e. A A. y e. B x = C ) ) $=
      ( wne cv wceq wrex wreu wral wb c0 wi wn wa wcel weu df-reu raleq reubidv
      cuni cint csn uniintsn eusn ral0 biantru 3bitr2i bitr4i syl6bb necon3bbid
      eubii biimprd reurex rexn0 rexlimivw necon2bi jctild pm5.21 syl6 r19.28zv
      wex syl eubidv reusv2lem4 equid biantrur rexbii reubii biantrurd pm5.32ri
      eleq1 syl6rbbr biimt bitrd imbi1i ralbiia 3bitr4i 3bitr4g pm2.61ine jaoi
      a1d ) CUCZCUDZGZAHZEIZBDJZACKZWIBDLZACKZMZDNGZWGWNODNDNIZWGWKPZWMPZQWNWPW
      GWRWQWPWRWGWPWMWEWFWPWMWIBNLZACKZWEWFIZWPWLWSACWIBDNUAUBWTWHCRZWSQZASZXAW
      SACTXACWHUEIAVDXBASXDACUFACUGXBXCAWSXBWIBUHUIUNUJUKULUMUOWKDNWKWJACJWOWJA
      CUPWJWOACWIBDUQURVEUSUTWKWMVAVBWOWNWGWOXBWIQZBDLZASZXBWLQZASWKWMWOXFXHAXB
      WIBDVCVFBHZXIIZWIQZBDJZACKECRZXJQZWIOZBDLZASWKXGXJABCDEVGWJXLACWIXKBDXJWI
      BVHZVIVJVKXFXPAXEXOBDXIDRZXEXMWIOZXOXRXEWIXSXRWIXMWIQXEXRXMWIFVLWIXBXMWHE
      CVNVMVOXRXMWIXSMFXMWIVPVEVQXMXNWIXJXMXQUIVRULVSUNVTWLACTWAZWDWBXTWC $.
      $( [14-Dec-2012] $)
  $}

  ${
    $d w x y z A $.  $d w x z B $.  $d x z C $.  $d w x z ph $.
    reusv8.1 $e |- ( ( y e. B /\ ph ) -> C e. A ) $.
    $( Two ways to express single-valuedness of a class expression
       ` C ( y ) ` .  Note that ` U. A = |^| A ` means ` A ` is a singleton
       ( ~ uniintsn ). $)
    reusv8OLD $p |- ( ( U. A =/= |^| A \/ E. y e. B ph )
          -> ( E! x e. A E. y e. B ( ph /\ x = C )
                   <-> E! x e. A A. y e. B ( ph -> x = C ) ) ) $=
      ( vz vw wne wrex cv wcel wa c0 wceq wreu wi wral reubii cuni cint wo crab
      cab wb rabn0 df-rab neeq1i bitr3i orbi2i csb wsbc df-clab sbimi sylbi cvv
      sbcel1g ax-mp sylib reusv7OLD hbab1 hbcsb1 hbeleq csbeq1a cbvrexf cbvralf
      vex ax-17 eqeq2d 3bitr4g abid anbi1i bitr2i rexbii2 imbi1i impexp ralbii2
      anass ) DUADUBJZACEKZUCVTCLZEMZANZCUEZOJZUCZABLZFPZNZCEKZBDQZAWIRZCESZBDQ
      ZUFWAWFVTWAACEUDZOJWFACEUGWPWEOACEUHUIUJUKWGWICWEKZBDQZWICWESZBDQZWLWOWGW
      HCHLZFULZPZHWEKZBDQXCHWESZBDQWRWTBHDWEXBXAWEMZFDMZCXAUMZXBDMZXFWDCXAUMXHW
      DHCUNWDXGCHGUOUPXAUQMXHXIUFHVHZCXAFDUQURUSUTVAWQXDBDWIXCCHIWEWDCIVBZILWEM
      HVIZWIHVIZCBXBCBXAFXJWHXAMCVIVCVDZWBXAPFXBWHCXAFVEVJZVFTWSXEBDWIXCCHIWEXK
      XLXMXNXOVGTVKWKWQBDWJWICEWEWBWEMZWINWDWINWCWJNXPWDWIWDCVLZVMWCAWIVSVNVOTW
      NWSBDWMWICEWEXPWIRWDWIRWCWMRXPWDWIXQVPWCAWIVQVNVRTVKUP $.
      $( [16-Dec-2012] $)
  $}

  ${
    $d x y z A $.  $d x z B $.
    eusvobj1.1 $e |- B e. _V $.
    $( Specify the same object in two ways when class ` B ( y ) ` is
       single-valued. $)
    eusvobj1 $p |- ( E! x E. y e. A x = B
       -> U. { x | E. y e. A x = B } = U. { x | A. y e. A x = B } ) $=
      ( vz cv wceq wrex weu wcel wex cab cuni wral wa cvv crab eqeq1 hbuni wreu
      eusv4 reuv ralbidv hbra1 ax-17 hbrab hbeleq ralbid reuuni3 sylbir rexbidv
      sylbi hbre1 rexbid r19.29 syl2anc eqtr3 cbvabv rabab eqtr4i unieqi ancoms
      3eqtr4g reximi syl df-rex hbab hbeq 19.41 bitri sylib simprd ) AGZDHZBCIZ
      AJZBGCKZBLZVPAMZNZVOBCOZAMZNZHZVQWEBCIZVSWEPZVQFGZDHZBCOZFQRZNZDHZWIBCIZF
      QRZNZDHZPZBCIZWFVQWMBCOZWQBCIZWSVQWBAJZWTABCDEUBXBWBAQUAWTWBAUCWBWJWTAFQV
      NWHHZVOWIBCVNWHDSZUDZVNWLHVOWMBCBAWLBAWKWJBFAQWIBCUEVNQKBUFZUGTUHVNWLDSUI
      UJUKUMVQVPAQUAXAVPAUCVPWNXAAFQXCVOWIBCXDULZVNWPHVOWQBCBAWPBAWOWNBFAQWIBCU
      NXFUGTUHVNWPDSUOUJUKWMWQBCUPUQWRWEBCWQWMWEWQWMPWPWLWAWDWPWLDURVTWOVTWNFMW
      OVPWNAFXGUSWNFUTVAVBWCWKWCWJFMWKWBWJAFXEUSWJFUTVAVBVDVCVEVFWFVRWEPBLWGWEB
      CVGVRWEBBFFWAWDBFVTVPBAFVOBCUNVHTBFWCWBBAFVOBCUEVHTVIVJVKVLVM $.
      $( [1-Nov-2010] $)

    $( Specify the same property in two ways when class ` B ( y ) ` is
       single-valued. $)
    eusvobj2 $p |- ( E! x E. y e. A x = B
       -> ( E. y e. A x = B <-> A. y e. A x = B ) ) $=
      ( cv wceq weu cab wcel cvv crab cuni rabab unieqi wreu reuv reuunisn abid
      csn wrex eusvobj1 3eqtr4g sneqd sylbir eusv4 bitr4i sylbi 3eqtr4d 3eqtr3g
      wral eleq2d 3bitr3g ) AFZDGZBCUAZAHZUNUPAIZJUNUOBCUKZAIZJUPUSUQURUTUNUQUP
      AKLZUSAKLZURUTUQVAMZTZVBMZTZVAVBUQVCVEUQURMUTMVCVEABCDEUBVAURUPANZOVBUTUS
      ANZOUCUDUQUPAKPVAVDGUPAQUPAKRUEUQUSAKPZVBVFGUQUSAHVIABCDEUFUSAQUGUSAKRUHU
      IVGVHUJULUPASUSASUM $.
      $( [29-Jul-2011] $) $( [1-Nov-2010] $)
  $}

  ${
    $d x A $.  $d y ph $.  $d x ps $.  $d x y $.
    alxfr.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    alxfr $p |- ( ( A. y A e. B /\ A. x E. y x = A ) ->
                ( A. x ph <-> A. y ps ) ) $=
      ( wcel wal cv wceq wex wa wi hba1 ax-17 cla4gv a4s alrimd adantr syl5ibr
      hbim ax-4 exlimi adantl impbid ) EFHZDIZCJEKZDLZCIZMACIZBDIZUHULUMNUKUHUL
      BDUGDOULDPUGULBNDABCEFGQRSTUKUMULNUHUKUMACUJCOUMCPUJUMANZCUIUNDUMADBDOADP
      UBUMAUIBBDUCGUAUDRSUEUF $.
      $( [18-Feb-2007] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x C $.  $d x ch $.  $d x y ph $.  $d y ps $.
    ralxfrd.1 $e |- ( ( ph /\ y e. C ) -> A e. B ) $.
    ralxfrd.2 $e |- ( ( ph /\ x e. B ) -> E. y e. C x = A ) $.
    ralxfrd.3 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrd $p |- ( ph -> ( A. x e. B ps <-> A. y e. C ch ) ) $=
      ( wral cv wcel wi ex rcla4dv com23 ralrimdv ax-17 syld wceq wrex wa hbra1
      hban ra4 exbiri sylan9r rexlimd syl5d impbid ) ABDGLZCEHLZAUMCEHAEMHNZUMC
      AUOFGNZUMCOZAUOUPIPAUPUQABCDFGKQPUARSAUNBDGADMZGNZURFUBZEHUCZUNBAUSVAJPAU
      NVABOAUNUDUTBEHAUNEAETCEHUEUFBETUNUOCAUTBOCEHUGAUTCBAUTBCKUHRUIUJPUKSUL
      $.
      $( [15-Aug-2014] $)

    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  (Contributed by FL,
       10-Apr-2007.) $)
    rexxfrd $p |- ( ph -> ( E. x e. B ps <-> E. y e. C ch ) ) $=
      ( wn wral wrex cv wceq wa notbid ralxfrd dfrex2 3bitr4g ) ABLZDGMZLCLZEHM
      ZLBDGNCEHNAUCUEAUBUDDEFGHIJADOFPQBCKRSRBDGTCEHTUA $.
      $( [15-Aug-2014] $) $( [10-Apr-2007] $)
  $}

  ${
    $d x y ph $.  $d x ch $.  $d y ps $.  $d x A $.  $d x y B $.
    ralxfrdOLD.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    ralxfrdOLD.2 $e |- ( ( ph /\ x e. B ) -> E. y e. B x = A ) $.
    ralxfrdOLD.3 $e |- ( ( ph /\ x = A ) -> ( ps <-> ch ) ) $.
    $( Obsolete version of ~ ralxfrd as of 14-Aug-2014. $)
    ralxfrdOLD $p |- ( ph -> ( A. x e. B ps <-> A. y e. B ch ) ) $=
      ( wral cv wcel wi ex rcla4dv syld com23 ralrimdv ax-17 wceq wrex wa hbra1
      hban ra4 exbiri sylan9r rexlimd syl5d impbid ) ABDGKZCEGKZAULCEGAELGMZULC
      AUNFGMZULCNZAUNUOHOAUOUPABCDFGJPOQRSAUMBDGADLZGMZUQFUAZEGUBZUMBAURUTIOAUM
      UTBNAUMUCUSBEGAUMEAETCEGUDUEBETUMUNCAUSBNCEGUFAUSCBAUSBCJUGRUHUIOUJSUK $.
      $( [17-Feb-2007] $)

    $( Obsolete version of ~ rexxfrd as of 14-Aug-2014. $)
    rexxfrdOLD $p |- ( ph -> ( E. x e. B ps <-> E. y e. B ch ) ) $=
      ( rexxfrd ) ABCDEFGGHIJK $.
      $( [10-Apr-2007] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.  $d x C $.
    ralxfr.1 $e |- ( y e. C -> A e. B ) $.
    ralxfr.2 $e |- ( x e. B -> E. y e. C x = A ) $.
    ralxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfr $p |- ( A. x e. B ph <-> A. y e. C ps ) $=
      ( cvv wceq wral wb eqid cv wcel adantl wrex ralxfrd ax-mp ) KKLZACFMBDGMN
      KOUBABCDEFGDPGQEFQUBHRCPZFQUCELZDGSUBIRUDABNUBJRTUA $.
      $( [15-Aug-2014] $) $( [10-Jun-2005] $)

    $( Transfer universal quantification from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    ralxfrALT $p |- ( A. x e. B ph <-> A. y e. C ps ) $=
      ( wral cv wcel wi rcla4v syl com12 ralrimiv wceq wrex hbra1 biimprcd syl6
      ax-17 ra4 rexlimd syl5 impbii ) ACFKZBDGKZUIBDGDLGMZUIBUKEFMUIBNHABCEFJOP
      QRUJACFCLZFMULESZDGTUJAIUJUMADGBDGUAADUDUJUKBUMANBDGUEUMABJUBUCUFUGRUH $.
      $( [15-Aug-2014] $) $( [10-Jun-2005] $)

    $( Transfer existence from a variable ` x ` to another variable ` y `
       contained in expression ` A ` . $)
    rexxfr $p |- ( E. x e. B ph <-> E. y e. C ps ) $=
      ( wn wral wrex cv wceq notbid ralxfr notbii dfrex2 3bitr4i ) AKZCFLZKBKZD
      GLZKACFMBDGMUBUDUAUCCDEFGHICNEOABJPQRACFSBDGST $.
      $( [15-Aug-2014] $) $( [10-Jun-2005] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    ralxfrOLD.1 $e |- ( y e. B -> A e. B ) $.
    ralxfrOLD.2 $e |- ( x e. B -> E. y e. B x = A ) $.
    ralxfrOLD.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Obsolete version of ~ ralxfr as of 14-Aug-2014. $)
    ralxfrOLD $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( ralxfr ) ABCDEFFGHIJ $.
      $( [10-Jun-2005] $)

    $( Obsolete version of ~ ralxfrALT as of 14-Aug-2014. $)
    ralxfrALTOLD $p |- ( A. x e. B ph <-> A. y e. B ps ) $=
      ( ralxfrALT ) ABCDEFFGHIJ $.
      $( [10-Jun-2005] $)

    $( Obsolete version of ~ rexxfr as of 14-Aug-2014. $)
    rexxfrOLD $p |- ( E. x e. B ph <-> E. y e. B ps ) $=
      ( rexxfr ) ABCDEFFGHIJ $.
      $( [10-Jun-2005] $)
  $}

  ${
    $d x A $.  $d z B $.  $d z C $.  $d x y z D $.  $d y ph $.  $d y z ps $.
    $d x z ch $.
    rabxfrd.1 $e |- ( z e. B -> A. y z e. B ) $.
    rabxfrd.2 $e |- ( z e. C -> A. y z e. C ) $.
    rabxfrd.3 $e |- ( ( ph /\ y e. D ) -> A e. D ) $.
    rabxfrd.4 $e |- ( x = A -> ( ps <-> ch ) ) $.
    rabxfrd.5 $e |- ( y = B -> A = C ) $.
    $( Class builder membership after substituting an expression ` A `
       (containing ` y ` ) for ` x ` in the class expression ` ch ` . $)
    rabxfrd $p |- ( ( ph /\ B e. D ) ->
                 ( C e. { x e. D | ps } <-> B e. { y e. D | ch } ) ) $=
      ( wcel crab wb wa wi cv ibibr sylib imp anbi1d elrab rabid 3bitr4g eleq2d
      rabbidva ax-17 hbel wceq eleq1d elrabf hbrab1 eleq1 3bitr3g pm5.32 sylibr
      ex ) AHJPZIBDJQZPZHCEJQZPZRZAVBVDSZVBVFSZRVBVGTAHGVCPZEJQZPHEUAZVEPZEJQZP
      VHVIAVKVNHAVJVMEJAVLJPZSZGJPZCSVOCSVJVMVPVQVOCAVOVQVORZAVOVQTVOVRTAVOVQMV
      AVOVQUBUCUDUEBCDGJNUFCEJUGUHUJUIVJVDEFHJKFUAZJPEUKZEFFIVCLVSVCPEUKULVLHUM
      GIVCOUNUOVMVFEFHJKVTEFFHVEKCEFJUPULVLHVEUQUOURVBVDVFUSUTUD $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x A $.  $d z B $.  $d z C $.  $d x y z D $.  $d y z ph $.  $d x z ps $.
    rabxfr.1 $e |- ( z e. B -> A. y z e. B ) $.
    rabxfr.2 $e |- ( z e. C -> A. y z e. C ) $.
    rabxfr.3 $e |- ( y e. D -> A e. D ) $.
    rabxfr.4 $e |- ( x = A -> ( ph <-> ps ) ) $.
    rabxfr.5 $e |- ( y = B -> A = C ) $.
    $( Class builder membership after substituting an expression ` A `
       (containing ` y ` ) for ` x ` in the class expression ` ph ` . $)
    rabxfr $p |- ( B e. D ->
                 ( C e. { x e. D | ph } <-> B e. { y e. D | ps } ) ) $=
      ( cv wceq wcel crab wb equid adantl rabxfrd mpan ) COZUDPZGIQHACIRQGBDIRQ
      SCTUEABCDEFGHIJKDOIQFIQUELUAMNUBUC $.
      $( [10-Jun-2005] $)
  $}

  ${
    $d x y ph $.  $d x ps $.  $d x A $.  $d x y B $.
    reuxfr2d.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    reuxfr2d.2 $e |- ( ( ph /\ x e. B ) -> E* y ( y e. B /\ x = A ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    reuxfr2d $p |- ( ph
        -> ( E! x e. B E. y e. B ( x = A /\ ps ) <-> E! y e. B ps ) ) $=
      ( cv wa wrex wreu wcel wmo wi syl ancom bitri mobii 2reuswap wceq impbid1
      wral moan anass sylib ralrimiva moeq moani an12 mpbi a1i mprg wb ceqsrexv
      biidd reubidva bitrd ) ACIZEUAZBJZDFKCFLZVACFKZDFLZBDFLAVBVDADIFMZVAJZDNZ
      CFUCVBVDOAVGCFAUSFMZJZBVEUTJZJZDNZVGVIVJDNVLHVJBDUDPVKVFDVKVJBJVFBVJQVEUT
      BUERSUFUGVACDFTPVHVAJZCNZVDVBODFVADCFTVNVEVHBJZUTJZCNVNUTVOCCEUHUIVPVMCVP
      UTVOJVMVOUTQUTVHBUJRSUKULUMUBAVCBDFAVEJEFMVCBUNGBBCEFUTBUPUOPUQUR $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x ph $.  $d x A $.  $d x y B $.
    reuxfr2.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr2.2 $e |- ( x e. B -> E* y ( y e. B /\ x = A ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` . $)
    reuxfr2 $p |- ( E! x e. B E. y e. B ( x = A /\ ph ) <-> E! y e. B ph ) $=
      ( cv wa wrex wreu wcel wmo wi 2reuswap syl ancom bitri mobii mprg wceq wb
      moan anass sylib moeq moani an12 mpbi a1i impbii biidd ceqsrexv reubiia )
      BHZDUAZAIZCEJBEKZUQBEJZCEKZACEKURUTCHELZUQIZCMZURUTNBEUQBCEOUOELZAVAUPIZI
      ZCMZVCVDVECMVGGVEACUCPVFVBCVFVEAIVBAVEQVAUPAUDRSUETVDUQIZBMZUTURNCEUQCBEO
      VIVAVDAIZUPIZBMVIUPVJBBDUFUGVKVHBVKUPVJIVHVJUPQUPVDAUHRSUIUJTUKUSACEVADEL
      USAUBFAABDEUPAULUMPUNR $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d x y ph $.  $d y ps $.  $d x ch $.  $d x A $.  $d x y B $.
    reuxfrd.1 $e |- ( ( ph /\ y e. B ) -> A e. B ) $.
    reuxfrd.2 $e |- ( ( ph /\ x e. B ) -> E! y e. B x = A ) $.
    reuxfrd.3 $e |- ( x = A -> ( ps <-> ch ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  Use ~ reuhypd to
       eliminate the second hypothesis. $)
    reuxfrd $p |- ( ph -> ( E! x e. B ps <-> E! y e. B ch ) ) $=
      ( wreu cv wceq wa wrex wcel reurex syl biantrurd r19.41v pm5.32i reubidva
      rexbii bitr3i syl6bb wmo weu df-reu eumo sylbi reuxfr2d bitrd ) ABDGKDLZF
      MZCNZEGOZDGKCEGKABUPDGAUMGPNZBUNEGOZBNZUPUQURBUQUNEGKZURIUNEGQRSUSUNBNZEG
      OUPUNBEGTVAUOEGUNBCJUAUCUDUEUBACDEFGHUQUTELGPUNNZEUFZIUTVBEUGVCUNEGUHVBEU
      IUJRUKUL $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d x ps $.  $d y ph $.  $d x A $.  $d x y B $.
    reuxfr.1 $e |- ( y e. B -> A e. B ) $.
    reuxfr.2 $e |- ( x e. B -> E! y e. B x = A ) $.
    reuxfr.3 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( Transfer existential uniqueness from a variable ` x ` to another
       variable ` y ` contained in expression ` A ` .  Use ~ reuhyp to
       eliminate the second hypothesis. $)
    reuxfr $p |- ( E! x e. B ph <-> E! y e. B ps ) $=
      ( wceq wreu wb eqid cv wcel adantl reuxfrd ax-mp ) FFJZACFKBDFKLFMSABCDEF
      DNFOEFOSGPCNZFOTEJDFKSHPIQR $.
      $( [14-Nov-2004] $)
  $}

  ${
    $d y ph $.  $d y B $.  $d y C $.  $d x y $.
    reuhypd.1 $e |- ( ( ph /\ x e. C ) -> B e. C ) $.
    reuhypd.2 $e |- ( ( ph /\ x e. C /\ y e. C ) -> ( x = A <-> y = B ) ) $.
    $( A theorem useful for eliminating the restricted existential uniqueness
       hypotheses in ~ riotaxfrd . $)
    reuhypd $p |- ( ( ph /\ x e. C ) -> E! y e. C x = A ) $=
      ( cv wcel wa wceq weu wreu cvv elex syl eueq sylib eleq1 syl5ibrcom 3expa
      pm4.71rd wb pm5.32da bitr4d eubidv mpbid df-reu sylibr ) ABIZFJZKZCIZFJZU
      KDLZKZCMZUPCFNUMUNELZCMZURUMEOJZUTUMEFJZVAGEFPQCERSUMUSUQCUMUSUOUSKUQUMUS
      UOUMUOUSVBGUNEFTUAUCUMUOUPUSAULUOUPUSUDHUBUEUFUGUHUPCFUIUJ $.
      $( [16-Jan-2012] $)
  $}

  ${
    $d y B $.  $d y C $.  $d x y $.
    reuhyp.1 $e |- ( x e. C -> B e. C ) $.
    reuhyp.2 $e |- ( ( x e. C /\ y e. C ) -> ( x = A <-> y = B ) ) $.
    $( A theorem useful for eliminating the restricted existential uniqueness
       hypotheses in ~ reuxfr . $)
    reuhyp $p |- ( x e. C -> E! y e. C x = A ) $=
      ( cv wceq wcel wreu equid adantl wb 3adant1 reuhypd mpan ) AHZRIZREJZRCIZ
      BEKALSABCDETDEJSFMTBHZEJUAUBDINSGOPQ $.
      $( [15-Nov-2004] $)
  $}

  ${
    $d x B $.  $d x z C $.  $d x y z A $.  $d ph y z $.  $d ps x z $.
    reuunixfr.1 $e |- ( z e. C -> A. y z e. C ) $.
    reuunixfr.2 $e |- ( y e. A -> B e. A ) $.
    reuunixfr.3 $e |- ( U. { y e. A | ps } e. A -> C e. A ) $.
    reuunixfr.4 $e |- ( x = B -> ( ph <-> ps ) ) $.
    reuunixfr.5 $e |- ( y = U. { y e. A | ps } -> B = C ) $.
    reuunixfr.6 $e |- ( x e. A -> E! y e. A x = B ) $.
    $( Change the variable ` x ` in the expression for "the unique ` A ` such
       that ` ph ` " to another variable ` y ` contained in expression ` B ` .
       Use ~ reuhyp to eliminate the last hypothesis. $)
    reuunixfr $p |- ( E! x e. A ph -> U. { x e. A | ph } = C ) $=
      ( wreu crab cuni cv wcel wb rabid baib rabbiia unieqi reuxfr reucl2 reucl
      wceq hbrab1 hbuni rabxfr syl mpbird sylbi baibr reubiia biimpi ax-17 hbel
      wal wi a1i eleq1 reuuni2f syl2anc mpbid syl5eqr ) ACFOZACFPZQCRZVISZCFPZQ
      ZHVLVIVKACFVKVJFSZAACFUAZUBUCUDVHHVISZVMHUHZVHBDFOZVPABCDGFJNLUEZVRVPBDFP
      ZQZVTSZBDFUFVRWAFSZVPWBTBDFUGZABCDEGWAHFDEVTBDEFUIUJIJLMUKULUMUNVHHFSZVKC
      FOZVPVQTVHVRWEVSVRWCWEWDKULUNVHWFAVKCFVKVNAVOUOUPUQVKVPCEFHERHSCURZVPVPCU
      TVAWECEEHVIWGACEFUIUSVBVJHVIVCVDVEVFVG $.
      $( [13-Jun-2005] $)
  $}

  $( The Axiom of Union and its converse.  A class is a set iff its union is a
     set. $)
  uniexb $p |- ( A e. _V <-> U. A e. _V ) $=
    ( cvv wcel cuni uniexg cpw wss pwuni pwexg ssexg sylancr impbii ) ABCZADZBC
    ZABEOANFZGPBCMAHNBIAPBJKL $.
    $( [11-Nov-2003] $)

  $( The Axiom of Power Sets and its converse.  A class is a set iff its power
     class is a set. $)
  pwexb $p |- ( A e. _V <-> ~P A e. _V ) $=
    ( cpw cvv wcel cuni uniexb unipw eleq1i bitr2i ) ABZCDJEZCDACDJFKACAGHI $.
    $( [11-Nov-2003] $)

  $( The union of the universe is the universe.  Exercise 4.12(c) of
     [Mendelson] p. 235. $)
  univ $p |- U. _V = _V $=
    ( cvv cpw cuni pwv unieqi unipw eqtr3i ) ABZCACAHADEAFG $.
    $( [14-Sep-2003] $)

  ${
    eldifpw.1 $e |- C e. _V $.
    $( Membership in a power class difference. $)
    eldifpw $p |- ( ( A e. ~P B /\ -. C C_ B ) ->
                   ( A u. C ) e. ( ~P ( B u. C ) \ ~P B ) ) $=
      ( cpw wcel wss wn wa cun elpwi unss1 cvv wb unexg mpan2 elpwg syl syl5ibr
      cdif mpd unss simpr sylbir con3i anim12i eldif sylibr ) ABEZFZCBGZHZIACJZ
      BCJZEZFZUMUIFZHZIUMUOUITFUJUPULURUJABGZUPABKUSUPUJUMUNGZABCLUJUMMFZUPUTNU
      JCMFVADACUIMOPUMUNMQRSUAUQUKUQUMBGZUKUMBKVBUSUKIUKACBUBUSUKUCUDRUEUFUMUOU
      IUGUH $.
      $( [25-Mar-2007] $)

    $( Membership in the power class of a union. $)
    elpwun $p |- ( A e. ~P ( B u. C ) <-> ( A \ C ) e. ~P B ) $=
      ( cun cpw wcel cvv cdif wb difex2 ax-mp sylibr wss elpwg difexg syl uncom
      elex sseq2i ssundif bitri syl6rbbr bitrd pm5.21nii ) ABCEZFZGZAHGZACIZBFZ
      GZAUGSULUJHGZUIUJUKSCHGUIUMJDACHKLMUIUHAUFNZULAUFHOUIULUJBNZUNUIUMULUOJAC
      HPUJBHOQUNACBEZNUOUFUPABCRTACBUAUBUCUDUE $.
      $( [26-Mar-2007] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( Membership in an extension of a power class. $)
    elpwunsn $p |- ( A e. ( ~P ( B u. { C } ) \ ~P B ) -> C e. A ) $=
      ( vx csn cun cpw cdif wcel wn wa eldif cv wrex wral elpwg wi sylbi imim2i
      wss dfss3 syl6bb notbid biimpa rexnal sylibr wceq elpwi ssel elsni orim2i
      wo elun ord imp3a syl eleq1 biimpd 3syl exp3a pm2.43b rexlimdv imp syldan
      com4r ) ABCEZFZGZBGZHIAVHIZAVIIZJZKZCAIZAVHVILVJVLDMZBIZJZDANZVNVMVPDAOZJ
      ZVRVJVLVTVJVKVSVJVKABTVSABVHPDABUAUBUCUDVPDAUEUFVJVRVNVJVQVNDAVJVOAIZVQVN
      QVJWAVQWAVNVJWAVQWAVNQZVJAVGTZWAVQKZVOCUGZQZWDWBQAVGUHWCWAVOVGIZQZWFAVGVO
      UIWHWAVQWEWGVQWEQZWAWGVPVOVFIZULZWIVOBVFUMWKVPWEWJWEVPVOCUJUKUNRSUOUPWEWB
      WDWEWAVNVOCAUQURSUSUTVEVAVBVCVDR $.
      $( [26-Mar-2007] $)
  $}

  ${
    op1stb.1 $e |- A e. _V $.
    $( Extract the first member of an ordered pair.  Theorem 73 of [Suppes]
       p. 42.  (See ~ op2ndb to extract the second member, ~ op1sta for an
       alternate version, and ~ op1st for the preferred version.) $)
    op1stb $p |- |^| |^| <. A , B >. = A $=
      ( cop cint csn cpr cin df-op inteqi snex prex intpr wss wceq snsspr1 mpbi
      df-ss 3eqtri intsn eqtri ) ABDZEZEAFZEAUCUDUCUDABGZGZEUDUEHZUDUBUFABIJUDU
      EAKABLMUDUENUGUDOABPUDUERQSJACTUA $.
      $( [25-Nov-2003] $)
  $}

  ${
    $d x y A $.
    iunpw.1 $e |- A e. _V $.
    $( An indexed union of a power class in terms of the power class of the
       union of its index.  Part of Exercise 24(b) of [Enderton] p. 33. $)
    iunpw $p |- ( E. x e. A x = U. A <-> ~P U. A = U_ x e. A ~P x ) $=
      ( vy cuni wceq wrex cpw ciun wcel sseq2 biimprcd reximdv com12 ssiun elpw
      cv wss eliun wa uniiun syl6sseqr impbid1 df-pw abeq2i bitri 3bitr4g eqrdv
      vex rexbii ssid uniex eleq2 syl5bbr mpbii sylib elssuni elpwi eqss sylibr
      anim12i ex reximia syl impbii ) AQZBEZFZABGZVGHZABVFHZIZFZVIDVJVLVIDQZVGR
      ZVNVFRZABGZVNVJJVNVLJZVIVOVQVOVIVQVOVHVPABVHVPVOVFVGVNKLMNVQVNABVFIVGABVF
      VNOABUAUBUCVNVGDUIPVRVNVKJZABGVQAVNBVKSVSVPABVPDVKDVFUDUEUJUFUGUHVMVGVKJZ
      ABGZVIVMVGVLJZWAVMVGVGRZWBVGUKWCVGVJJVMWBVGVGBCULPVJVLVGUMUNUOAVGBVKSUPVT
      VHABVFBJZVTVHWDVTTVFVGRZVGVFRZTVHWDWEVTWFVFBUQVGVFURVAVFVGUSUTVBVCVDVE $.
      $( [29-Nov-2003] $)
  $}

  ${
    $d w v x $.  $d w v y $.  $d w v z $.  $d w v R $.  $d w v A $.
    $( A founded relation has no 3-cycle loops.  Special case of Proposition
       6.23 of [TakeutiZaring] p. 30. $)
    fr3nr $p |- ( ( R Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x R y /\ y R z /\ z R x ) ) $=
      ( vw vv cv wcel w3a wbr vex cab cin c0 wceq wne brab1 inelcm mpan wfr ctp
      wss wn 3anrot tpss bitri wrex tpnz tpex frc mp3an3 eltp tpid3 sylbi breq2
      wa w3o abbidv ineq2d syl5ibrcom tpid1 tpid2 3jaao com12 necon2bd rexlimiv
      neeq1d syl5bi syl sylan2b ) AHZDIZBHZDIZCHZDIZJZDEUAZVNVPVLUBZDUCZVLVNEKZ
      VNVPEKZVPVLEKZJZUDZVRVOVQVMJWAVMVOVQUEVNVPVLDBLZCLZALZUFUGVSWAUQVTFHZGHZE
      KZFMZNZOPZGVTUHZWFVSWAVTOQWPVNVPVLWGUIGFDVTEVNVPVLUJUKULWOWFGVTWKVTIZWEWN
      OWEWQWNOQZWQWKVNPZWKVPPZWKVLPZURWEWRWKVNVPVLGLUMWBWSWRWCWTWDXAWBWRWSVTWJV
      NEKZFMZNZOQZWBVLXCIZXEAFVNERVLVTIXFXEVNVPVLWIUNVLVTXCSTUOWSWNXDOWSWMXCVTW
      SWLXBFWKVNWJEUPUSUTVHVAWCWRWTVTWJVPEKZFMZNZOQZWCVNXHIZXJBFVPERVNVTIXKXJVN
      VPVLWGVBVNVTXHSTUOWTWNXIOWTWMXHVTWTWLXGFWKVPWJEUPUSUTVHVAWDWRXAVTWJVLEKZF
      MZNZOQZWDVPXMIZXOCFVLERVPVTIXPXOVNVPVLWHVCVPVTXMSTUOXAWNXNOXAWMXMVTXAWLXL
      FWKVLWJEUPUSUTVHVAVDVIVEVFVGVJVK $.
      $( [10-Apr-1994] $)
  $}

  $( A set founded by epsilon contains no 3-cycle loops. $)
  epne3 $p |- ( ( _E Fr A /\ ( x e. A /\ y e. A /\ z e. A ) ) ->
                -. ( x e. y /\ y e. z /\ z e. x ) ) $=
    ( cep wfr cv wcel w3a wa wbr fr3nr epel 3anbi123i sylnib ) DEFAGZDHBGZDHCGZ
    DHIJPQEKZQREKZRPEKZIPQHZQRHZRPHZIABCDELSUBTUCUAUDABMBCMCAMNO $.
    $( [19-Apr-1994] $)

  ${
    $d x y z R $.  $d x y z A $.
    $( Alternate definition of well-ordering.  Definition 6.24(2) of
       [TakeutiZaring] p. 30.  (The proof was shortened by Andrew Salmon,
       12-Aug-2011.) $)
    dfwe2 $p |- ( R We A <-> ( R Fr A /\ A. x e. A A. y e. A
                ( x R y \/ x = y \/ y R x ) ) ) $=
      ( vz wa cv wbr wceq w3o wral wn wi wcel w3a wal breq2 r3al 3imtr4g ralbii
      wwe wfr wor df-we df-so simpr ax-1 fr2nr 3adantr3 anbi2d notbid syl5ibcom
      wpo a1i pm2.21 syl6 fr3nr df-3an biimpri ancoms pm2.21d exp3a 3jaod frirr
      nsyl 3ad2antr1 jctild a2d alimdv 2alimdv ralidm equequ2 3orbi123d cbvralv
      ex breq1 bitr3i df-po ancrd impbid2 syl5bb pm5.32i bitri ) CDUACDUBZCDUCZ
      FWDAGZBGZDHZWFWGIZWGWFDHZJZBCKZACKZFCDUDWDWEWMWECDUMZWMFZWDWMABCDUEWDWOWM
      WNWMUFWDWMWNWDWFEGZDHZWFWPIZWPWFDHZJZECKZBCKZACKZWFWFDHLZWHWGWPDHZFZWQMZF
      ZECKBCKACKZWMWNWDWFCNZWGCNZWPCNZOZWTMZEPZBPAPXMXHMZEPZBPAPXCXIWDXOXQABWDX
      NXPEWDXMWTXHWDXMWTXHMWDXMFZWTXGXDXRWQXGWRWSWQXGMXRWQXFUGUNXRWRXFLZXGXRWHW
      JFZLZWRXSWDXJXKYAXLABCDUHUIWRXTXFWRWJXEWHWFWPWGDQUJUKULXFWQUOUPXRWSXFWQXR
      WSXFFZWQXRWHXEWSOZYBABECDUQXFWSYCYCXFWSFWHXEWSURUSUTVEVAVBVCWDXKXJXDXLACD
      VDVFVGVOVHVIVJWTABECCCRXHABECCCRSWLXBACWLWLBCKXBWKBCVKWLXABCWKWTBECWGWPIW
      HWQWIWRWJWSWGWPWFDQBEAVLWGWPWFDVPVMVNTVQTABECDVRSVSVTWAWBWC $.
      $( [12-Aug-2011] $) $( [16-Mar-1997] $)
  $}


