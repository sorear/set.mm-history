$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
            Equinumerosity (cont.)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d A a c $.  $d B a c $.  $d C c $.  $d D a c $.
    xpen.1 $e |- B e. _V $.
    xpen.2 $e |- D e. _V $.
    $( Equinumerosity law for cross product.  Proposition 4.22(b) of
       [Mendelson] p. 254.  (Revised by Mario Carneiro, 9-Mar-2013.) $)
    xpen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) $=
      ( va vc cvv wcel wa cen wbr cxp cdom xpdom2 wb sbthbg ax-mp xpex brrelexi
      relen anim12i cv wi wceq breq1 anbi1d xpeq1 breq1d imbi12d anbi2d 3imtr3i
      xpeq2 vex xpcomen enen2 mp2an enen1 bitri sylib entr syl2an vtocl2g mpcom
      ) AIJZCIJZKABLMZCDLMZKZACNZBDNZLMZVHVFVIVGABLUBUACDLUBUAUCGUDZBLMZHUDZDLM
      ZKZVNVPNZVLLMZUEVHVQKZAVPNZVLLMZUEVJVMUEGHACIIVNAUFZVRWAVTWCWDVOVHVQVNABL
      UGUHWDVSWBVLLVNAVPUIUJUKVPCUFZWAVJWCVMWEVQVIVHVPCDLUGULWEWBVKVLLVPCAUNUJU
      KVOVSBVPNZLMZWFVLLMZVTVQVOVPVNNZVPBNZLMZWGVNBOMZBVNOMZKZWIWJOMZWJWIOMZKZV
      OWKWLWOWMWPVNBVPEHUOZPBVNVPGUOZWRPUCBIJWNVOQEVNBIRSWJIJWQWKQVPBWRETWIWJIR
      SUMWKWIWFLMZWGWFIJWJWFLMWKWTQBVPEWRTVPBWREUPWJWFWIIUQURVSIJWIVSLMWTWGQVNV
      PWSWRTVPVNWRWSUPWIVSWFIUSURUTVAVPDOMZDVPOMZKZWFVLOMZVLWFOMZKZVQWHXAXDXBXE
      VPDBFEPDVPBWREPUCDIJXCVQQFVPDIRSVLIJXFWHQBDEFTWFVLIRSUMVSWFVLVBVCVDVE $.
      $( [9-Mar-2013] $) $( [24-Jul-2004] $)
  $}

  ${
    $d A b d $.  $d B b d $.  $d C b d $.  $d D d $.
    $( Equinumerosity law for cross product.  (Contributed by Jeff Madsen,
       17-Jun-2010.) $)
    xpeng $p |- ( ( B e. V /\ D e. W ) ->
                    ( ( A ~~ B /\ C ~~ D ) -> ( A X. C ) ~~ ( B X. D ) ) ) $=
      ( vb vd cv cen wbr wa cxp wi wceq breq2 anbi1d breq2d imbi12d vex vtocl2g
      xpeq1 anbi2d xpeq2 xpen ) AGIZJKZCHIZJKZLZACMZUFUHMZJKZNABJKZUILZUKBUHMZJ
      KZNUNCDJKZLZUKBDMZJKZNGHBDEFUFBOZUJUOUMUQVBUGUNUIUFBAJPQVBULUPUKJUFBUHUBR
      SUHDOZUOUSUQVAVCUIURUNUHDCJPUCVCUPUTUKJUHDBUDRSAUFCUHGTHTUEUA $.
      $( [17-Jun-2010] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.
    $( The cartesian product of numerable sets is numerable.  (Contributed by
       Mario Carneiro, 3-Mar-2013.) $)
    xpnum $p |- ( ( E. x e. On x ~~ A /\ E. x e. On x ~~ B ) ->
      E. x e. On x ~~ ( A X. B ) ) $=
      ( vy vz cv cen wbr con0 wrex cxp breq1 cbvrexv wa cvv wcel wi syl2anc imp
      c0 comu co simp1l simp2l omcl omxpen simp3l simp3r simp1r simp2r syl22anc
      w3a xpeng entr rcla4ev 3expia exp31 rexlimiv exp3a rexlimdv wne 0elon 0ex
      enref brprc mpbiri sylancr con1i wceq onenon ax-mp breq2 rexbidv necon3bi
      wn xpexr2 pm2.61d1 syl2anb ) AFZBGHZAIJDFZBGHZDIJZEFZCGHZEIJZVSBCKZGHZAIJ
      ZVSCGHZAIJVTWBADIVSWABGLMWJWEAEIVSWDCGLMWCWFNBOPZCOPZNZWIWCWFWMWIQZWCWEWN
      EIWCWDIPZWEWNWBWOWENZWNQDIWAIPZWBWPWNWQWBNZWPWMWIWRWPWMULZWAWDUAUBZIPZWTW
      GGHZWIWSWQWOXAWQWBWPWMUCZWRWOWEWMUDZWAWDUERWSWTWAWDKZGHZXEWGGHZXBWSWQWOXF
      XCXDWAWDUFRWSWKWLWBWEXGWRWPWKWLUGWRWPWKWLUHWQWBWPWMUIWRWOWEWMUJWMWBWENXGW
      ABWDCOOUMSUKWTXEWGUNRWHXBAWTIVSWTWGGLUORUPUQURUSUTSWIWMWIVOWGOPZWGTVAWMXH
      WIXHVOZTIPZTWGGHZWIVBXIXKTTGHTVCVDTWGGVEVFWHXKATIVSTWGGLUOVGVHWIWGTWGTVIZ
      WIVSTGHZAIJZXJXNVBATVJVKXLWHXMAIWGTVSGVLVMVFVNBCOVPRVHVQVR $.
      $( [3-Mar-2013] $)
  $}

  ${
    $d f g x y z w v A $.  $d f g x y z w v B $.  $d f g x y z w v C $.
    $d f g x y z w v D $.  $d z w v H $.
    mapenlem.1 $e |- A e. _V $.
    mapenlem.2 $e |- B e. _V $.
    mapenlem.3 $e |- C e. _V $.
    mapenlem.4 $e |- D e. _V $.
    mapenlem.5 $e |- H =
         { <. x , y >. | ( x e. ( A ^m C ) /\ y = ( ( f o. x ) o. `' g ) ) } $.
    $( Lemma for ~ mapen . $)
    mapenlem1 $p |- ( ( ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) /\ z :
    C --> A ) /\ v e. C ) -> ( ( H ` z ) ` ( g ` v ) ) = ( f ` ( z ` v ) ) ) $=
      ( cv cfv ccom wceq wf1o wa wf wcel ccnv cmap elmap coeq2 coeq1d vex cnvex
      co coex fvopab4 sylbir fveq1d ad2antlr coass f1of fco sylan cid f1ococnv1
      coeq2d fcoi1 sylan9eqr an32s syl5eq adantr fvco3 adantll 3eqtr3d eqtrd
      cres ) EFIQZUAZGHJQZUAZUBZGECQZUCZUBZDQZGUDZUBZWCVQRZVTKRZRZWFVOVTSZVQUEZ
      SZRZWCVTRVORZWAWHWLTVSWDWAWFWGWKWAVTEGUFULZUDWGWKTEGVTLNUGABVTVOAQZSZWJSW
      KWNKWOVTTWPWIWJWOVTVOUHUIPWIWJVOVTIUJCUJUMVQJUJUKUMUNUOUPUQWEWCWKVQSZRZWC
      WIRZWLWMWBWRWSTWDWBWCWQWIWBWQWIWJVQSZSZWIWIWJVQURVPWAVRXAWITZVPWAUBGFWIUC
      ZVRXBVPEFVOUCWAXCEFVOUSGEFVOVTUTVAVRXCXAWIVBGVNZSWIVRWTXDWIGHVQVCVDGFWIVE
      VFVAVGVHUPVIWBGHVQUCZWDWRWLTVRXEVPWAGHVQUSUQGHWCWKVQVJVAWAWDWSWMTVSGEWCVO
      VTVJVKVLVM $.
      $( [26-Dec-2014] $) $( [16-Dec-2003] $)

    $( Lemma for ~ mapen . $)
    mapenlem2 $p |- ( ( f : A -1-1-onto-> B /\ g : C -1-1-onto-> D ) ->
             H : ( A ^m C ) -1-1-onto-> ( B ^m D ) ) $=
      ( wa wceq wf cfv ccom wcel vz vw vv cv wf1o cmap co wf1 wi wral ccnv f1of
      crn fco sylan f1ocnv syl syl2an exp31 com23 elmap 3imtr4g ralrimiv fopab2
      sylib anbi12i fveq1 adantl ad2antlr mapenlem1 adantrl exp43 adantrd imp42
      imp adantld 3eqtr3d wb f1of1 ffvelrn adantlr adantll f1fveq anassrs mpbid
      jca ralrimiva wfn eqfnfv2 ad2antrl mpbird exp32 syl5bi ralrimivv sylanbrc
      ffn dff13 wss frn wrex an32s sylibr coeq2 coeq1d cnvex coex fvopab4 coass
      vex coeq2i coeq1i eqtr2i 3eqtri cid f1ococnv2 fcoi2 sylan9eq coeq2d fcoi1
      cres eqtrd syl5eq ex fveq2 eqeq1d rcla4ev syl6 fvelrnb 3syl sylibrd ssrdv
      eqssd dff1o5 ) CDGUDZUEZEFHUDZUEZOZCEUFUGZDFUFUGZIUHZIUMZYTPYSYTIUEYRYSYT
      IQZUAUDZIRZUBUDZIRZPZUUDUUFPZUIZUBYSUJUAYSUJUUAYRYNAUDZSZYPUKZSZYTTZAYSUJ
      UUCYRUUOAYSYRECUUKQZFDUUNQZUUKYSTUUOYOYQUUPUUQUIYOUUPYQUUQYOUUPYQUUQYOUUP
      OEDUULQZFEUUMQZUUQYQYOCDYNQUUPUURCDYNULECDYNUUKUNUOYQFEUUMUEUUSEFYPUPFEUU
      MULUQFEDUULUUMUNURUSUTVOCEUUKJLVADFUUNKMVAVBVCABYSYTUUNINVDVEZYRUUJUAUBYS
      YSUUDYSTZUUFYSTZOECUUDQZECUUFQZOZYRUUJUVAUVCUVBUVDCEUUDJLVACEUUFJLVAVFYRU
      VEUUHUUIYRUVEUUHOZOZUUIUCUDZUUDRZUVHUUFRZPZUCEUJZUVGUVKUCEUVGUVHETZOZUVIY
      NRZUVJYNRZPZUVKUVNUVHYPRZUUERZUVRUUGRZUVOUVPUVFUVSUVTPZYRUVMUUHUWAUVEUVRU
      UEUUGVGVHVIYRUVEUUHUVMUVSUVOPZYRUVCUUHUVMUWBUIUIUVDYRUVCUUHUVMUWBYRUVCOUV
      MUWBUUHABUAUCCDEFGHIJKLMNVJVKVLVMVNYRUVEUUHUVMUVTUVPPZYRUVDUUHUVMUWCUIUIU
      VCYRUVDUUHUVMUWCYRUVDOUVMUWCUUHABUBUCCDEFGHIJKLMNVJVKVLVPVNVQYRUVFUVMUVQU
      VKVRZYOUVFUVMOZUWDYQYOCDYNUHUVICTZUVJCTZOZUWDUWECDYNVSUVEUVMUWHUUHUVEUVMO
      UWFUWGUVCUVMUWFUVDECUVHUUDVTWAUVDUVMUWGUVCECUVHUUFVTWBWFWACDUVIUVJYNWCURW
      AWDWEWGUVEUUIUVLVRZYRUUHUVCUUDEWHUUFEWHUWIUVDECUUDWPECUUFWPUCEUUDUUFWIURW
      JWKWLWMWNUAUBYSYTIWQWOYRUUBYTYRUUCUUBYTWRUUTYSYTIWSUQYRUAYTUUBYRUUDYTTZUU
      GUUDPZUBYSWTZUUDUUBTZYRUWJYNUKZUUDSZYPSZYSTZUWPIRZUUDPZOZUWLUWJFDUUDQZYRU
      WTDFUUDKMVAYRUXAUWTYRUXAOZUWQUWSUXBECUWPQZUWQYOUXAYQUXCYOUXAOZFCUWOQZEFYP
      QUXCYQYODCUWNQZUXAUXEYODCUWNUEUXFCDYNUPDCUWNULUQFDCUWNUUDUNUOEFYPULEFCUWO
      YPUNURXACEUWPJLVAXBZUXBUWRYNUWPSZUUMSZUUDUXBUWQUWRUXIPUXGABUWPUUNUXIYSIUU
      KUWPPUULUXHUUMUUKUWPYNXCXDNUXHUUMYNUWPGXIZUWOYPUWNUUDYNUXJXEUAXIXFHXIZXFX
      FYPUXKXEXFXGUQUXBUXIYNUWNSZUUDSZYPUUMSZSZUUDUXIYNUWPUUMSZSYNUWOUXNSZSZUXO
      YNUWPUUMXHUXPUXQYNUWOYPUUMXHXJUXOYNUWOSZUXNSUXRUXMUXSUXNYNUWNUUDXHXKYNUWO
      UXNXHXLXMUXBUXOUUDUXNSZUUDYOUXAUXOUXTPYQUXDUXMUUDUXNYOUXAUXMXNDXTZUUDSUUD
      YOUXLUYAUUDCDYNXOXDFDUUDXPXQXDWAYQUXAUXTUUDPYOYQUXAUXTUUDXNFXTZSUUDYQUXNU
      YBUUDEFYPXOXRFDUUDXSXQWBYAYBYAWFYCWMUWKUWSUBUWPYSUUFUWPPUUGUWRUUDUUFUWPIY
      DYEYFYGYRUUCIYSWHUWMUWLVRUUTYSYTIWPUBYSUUDIYHYIYJYKYLYSYTIYMWO $.
      $( [16-Dec-2003] $)
  $}

  ${
    $d A a c $.  $d B a c f g x y $.  $d C c $.  $d D a c f g x y $.
    mapen.1 $e |- B e. _V $.
    mapen.2 $e |- D e. _V $.
    $( Two set exponentiations are equinumerous when their bases and exponents
       are equinumerous.  Theorem 6H(c) of [Enderton] p. 139.  (Revised by
       Mario Carneiro, 9-Mar-2013.) $)
    mapen $p |- ( ( A ~~ B /\ C ~~ D ) -> ( A ^m C ) ~~ ( B ^m D ) ) $=
      ( va vc vf vg vx vy cvv wa cen wbr cmap co cv wi wcel relen brrelexi wceq
      anim12i breq1 anbi1d oveq1 breq1d imbi12d anbi2d oveq2 wf1o wex bren ccom
      ccnv copab vex eqid mapenlem2 ovex syl ex exlimiv exlimdv syl2anb vtocl2g
      f1oen imp mpcom ) AMUAZCMUAZNABOPZCDOPZNZACQRZBDQRZOPZVNVLVOVMABOUBUCCDOU
      BUCUEGSZBOPZHSZDOPZNZVTWBQRZVROPZTVNWCNZAWBQRZVROPZTVPVSTGHACMMVTAUDZWDWG
      WFWIWJWAVNWCVTABOUFUGWJWEWHVROVTAWBQUHUIUJWBCUDZWGVPWIVSWKWCVOVNWBCDOUFUK
      WKWHVQVROWBCAQULUIUJWAVTBISZUMZIUNZWBDJSZUMZJUNZWFWCVTBIEUOWBDJFUOWNWQWFW
      NWPWFJWMWPWFTIWMWPWFWMWPNWEVRKSZWEUALSWLWRUPWOUQUPUDNKLURZUMWFKLVTBWBDIJW
      SGUSEHUSFWSUTVAWEVRWSVTWBQVBVIVCVDVEVFVJVGVHVK $.
      $( [9-Mar-2013] $) $( [16-Dec-2003] $)
  $}

  ${
    $d A a $.  $d B a x $.  $d C a x $.
    mapdom1.1 $e |- B e. _V $.
    mapdom1.2 $e |- C e. _V $.
    $( Order-preserving property of set exponentiation.  Theorem 6L(c) of
       [Enderton] p. 149.  (Revised by Mario Carneiro, 9-Mar-2013.) $)
    mapdom1 $p |- ( A ~<_ B -> ( A ^m C ) ~<_ ( B ^m C ) ) $=
      ( va vx cvv wcel cdom wbr cmap co reldom brrelexi cv wi wceq cen wss ovex
      breq1 oveq1 breq1d imbi12d wa wex domen enref vex mapen mpan2 mapss mpsyl
      ssdomg endomtr syl2an exlimiv sylbi vtoclg mpcom ) AHIABJKZACLMZBCLMZJKZA
      BJNOFPZBJKZVFCLMZVDJKZQVBVEQFAHVFARZVGVBVIVEVFABJUBVJVHVCVDJVFACLUCUDUEVG
      VFGPZSKZVKBTZUFZGUGVIGVFBDUHVNVIGVLVHVKCLMZSKZVOVDJKZVIVMVLCCSKVPCEUIVFVK
      CCGUJEUKULVOHIVMVOVDTVQVKCLUAVKBCDEUMVOVDHUOUNVHVOVDUPUQURUSUTVA $.
      $( [9-Mar-2013] $) $( [27-Jul-2004] $)
  $}

  ${
    mapdom2.1 $e |- A e. _V $.
    mapdom2.2 $e |- B e. _V $.
    mapdom2.3 $e |- C e. _V $.
    $( Lemma for ~ mapdom2 .  (Unnecessary distinct variable restrictions were
       removed by David Abernethy, 1-Jan-2014.) $)
    mapdom2lem $p |- ( x e. ( C ^m z ) ->
                     ( x i^i ( ( B \ z ) X. { w } ) ) = (/) ) $=
      ( cv cmap co cin cdm c0 wceq wss vex wrel ax-mp wcel csn cxp wf elmap fdm
      cdif sylbi fconst fdmi a1i ineq12d disjdif syl6eq dmin sseq2 mpbii ss0 wb
      3syl relxp relin1 incom releqi mpbi reldm0 sylibr ) AJZFBJZKLUAZVHEVIUGZC
      JZUBZUCZMZNZOPZVOOPZVJVHNZVNNZMZOPZVPOQZVQVJWAVIVKMOVJVSVIVTVKVJVIFVHUDVS
      VIPFVIVHIBRUEVIFVHUFUHVTVKPVJVKVMVNVKVLCRUIUJUKULVIEUMUNWBVPWAQWCVHVNUOWA
      OVPUPUQVPURUTVOSZVRVQUSVNVHMZSZWDVNSWFVKVMVAVNVHVBTWEVOVNVHVCVDVEVOVFTVG
      $.
      $( [1-Jan-2014] $) $( [23-Sep-2004] $)

    $d z w A $.  $d x y z w B $.  $d x y z w C $.
    $( Order-preserving property of set exponentiation.  Theorem 6L(d) of
       [Enderton] p. 149. $)
    mapdom2 $p |- ( ( A ~<_ B /\ -. ( A = (/) /\ C = (/) ) ) ->
                  ( C ^m A ) ~<_ ( C ^m B ) ) $=
      ( vw vz vx c0 wceq wa cdom wbr cmap co cv wcel cun wf vy wn exmid biantru
      wo ianor ordir 3bitr4i wi oveq1 wne df-ne map0b sylbir sylan9eqr syl6eqbr
      0dom a1i wex neq0 cen wss domen enref vex mapen mpan cvv ovex cdif fconst
      csn cxp cin disjdif fun mpan2 wb undif feq2 sylbi snssi ssequn2 sylib syl
      feq3 sylan9bb syl5ib elmap 3imtr4g eqcomd sylan9eq biantrud unineq syl6bb
      mapdom2lem dom2d mpi endomtr syl2an expr exlimiv exlimdv jaod imp sylan2b
      syl5bi ) AJKZCJKZLUBZABMNZXHUBZXILZXIUBZUEZCAOPZCBOPZMNZXLXNUEZXSXIXNUEZL
      XJXOXTXSXIUCUDXHXIUFXLXIXNUGUHXKXOXRXKXMXRXNXMXRUIXKXMXPJXQMXIXLXPJAOPZJC
      JAOUJXLAJUKYAJKAJULADUMUNUOXQUQUPURXNGQZCRZGUSXKXRGCUTXKYCXRGXKAHQZVANZYD
      BVBZLZHUSYCXRUIZHABEVCYGYHHYEYFYCXRYEXPCYDOPZVANZYIXQMNZXRYFYCLZCCVANYEYJ
      CFVDCCAYDFHVEZVFVGYLYIVHRYKCYDOVIYLIUAYIXQIQZBYDVJZYBVLZVMZSZUAQZYQSZVHYL
      YDCYNTZBCYRTZYNYIRZYRXQRUUAYDYOSZCYPSZYRTZYLUUBUUAYOYPYQTZUUFYOYBGVEVKUUA
      UUGLYDYOVNJKUUFYDBVOYDYOCYPYNYQVPVQVQYFUUFBUUEYRTZYCUUBYFUUDBKUUFUUHVRYDB
      VSUUDBUUEYRVTWAYCUUECKZUUHUUBVRYCYPCVBUUIYBCWBYPCWCWDUUECBYRWFWEWGWHCYDYN
      FYMWICBYRFEWIWJUUCYSYIRZLZYRYTKZYNYSKZVRUIYLUUKUULUULYNYQVNZYSYQVNZKZLUUM
      UUKUUPUULUUCUUJUUNJUUOIHGABCDEFWPUUJUUOJUAHGABCDEFWPWKWLWMYNYSYQWNWOURWQW
      RXPYIXQWSWTXAXBWAXCXGXDXEXF $.
      $( [23-Sep-2004] $)
  $}

  ${
    $d x y z w v f g A $.  $d x y z w v f g B $.  $d x y z w v f g C $.
    mapxpen.1 $e |- A e. _V $.
    mapxpen.2 $e |- B e. _V $.
    mapxpen.3 $e |- C e. _V $.
    $( Equinumerosity law for double set exponentiation.  Proposition 10.45 of
       [TakeutiZaring] p. 96. $)
    mapxpen $p |- ( ( A ^m B ) ^m C ) ~~ ( A ^m ( B X. C ) ) $=
      ( vx vy vz vw vv vf vg cv wcel wa wceq cvv wfn ax-17 cmap co copab2 copab
      cxp cfv ovex eqid oprabex2 opabex2 wral fvex fnoprab2 fneq1 mpbiri adantl
      a1i hboprab1 hbeleq hban hboprab2 elmap ffvelrn sylanb sylan an32s anasss
      wf adantlr oveq ov4g mp3an3 sylan9eq eleq1d adantll exp32 ralrimd ralrimi
      wb mpbird xpex ffnov bitri sylanbrc fopabfv simplbi adantr anassrs eqeq2d
      sylbi pm5.32da opabbid syl5ibr mpd eqtrd jca hbopab1 hbopab fveq1 fvopab2
      wi mpan2 fveq1d oprabbid syl6bbr anbi1d biimparc ancomd fnopab2 biantrurd
      ffnfv bitr2i syl6bb ralbida anbi2d syl5bb syl bitr4d bitr3d ralcom impbii
      fnov en2 ) GHABUAUBZCUAUBZABCUEZUAUBZINZBOZJNZCOZPZKNZYHYJGNZUFZUFZQZPZIJ
      KUCZYKLNZYIMNZYHYJHNZUBZQZPZIMUDZQZPZJLUDZYDCUAUGYSROYNYEOZIJKBCYPYSEFYSU
      HZUIUQUUIROUUBYGOZJLCUUFFUJUQUUJUUBYSQZPZUULYNUUIQZPZUUNUULUUOUUNUUBYFSZU
      UCAOZJCUKZIBUKZUULUUMUUQUUJUUMUUQYSYFSIJKBCYPYSYHYOULZUUKUMYFUUBYSUNUOUPU
      UNUUSIBUUJUUMIUUJITIHYSYRIJKHURUSZUTUUNYIUURJCUUJUUMJUUJJTJHYSYRIJKHVAUSU
      TZYIJTUUNYIYKUURUUNYLPUURYPAOZUUJYLUVDUUMUUJYIYKUVDUUJYKYIUVDUUJYKPYOYDOZ
      YIUVDUUJCYDYNVHZYKUVEYDCYNABUAUGFVBZCYDYJYNVCVDZUVEBAYOVHZYIUVDABYODEVBZB
      AYHYOVCVDVEVFVGVIUUMYLUURUVDVSUUJUUMYLPUUCYPAUUMYLUUCYHYJYSUBZYPYHYJUUBYS
      VJYIYKYPROUVKYPQUVAIJKBCYPRYSUUKVKVLVMZVNVOVTVPVQVRUULYFAUUBVHUUQUUTPZAYF
      UUBDBCEFWAVBIJBCAUUBWBWCZWDUUNYNYKYTYOQZPZJLUDZUUIUUJYNUVQQZUUMUUJUVFUVRU
      VGUVFUVRUVEJCUKZJLCYDYNWEWFWJWGUUNUVPUUHJLUVCUUNLTUUNYKUVOUUGUUNYKPZYOUUF
      YTUVTUVEYOUUFQZUUJYKUVEUUMUVHVIUUMYKUVEUWAXAUUJUVEUWAUUMYKPZYOYIUUAYPQZPZ
      IMUDZQZUVEUVIUWFUVJUVIUWFUVDIBUKZIMBAYOWEWFWJUWBUUFUWEYOUWBUUEUWDIMUUMYKI
      UVBYKITZUTUWBMTUWBYIUUDUWCUWBYIPUUCYPUUAUUMYIYKUUCYPQZUUMYIYKUWIUVLWHVFWI
      WKWLWIWMVOWNWIWKWLWOWPUUPUUNUUTUUMPUUPUUMUUTUUOUUMUUTPZUULUUOUWJUVMUULUUO
      UUMUUQUUTUUOUUMUUBYLYMUUCQZPZIJKUCZQUUQUUOYSUWMUUBUUOYRUWLIJKIGUUIUUHJLIG
      YKUUGIUWHILUUFUUEIMLWQUSUTWRUSZJGUUIUUHJLGWQUSZUUOKTUUOYLYQUWKUUOYLPYPUUC
      YMUUOYIYKYPUUCQZUUOYKYIUWPUUOYKPZYIYPYHUUFUFZUUCUWQYHYOUUFUUOYKYOYJUUIUFZ
      UUFYJYNUUIWSYKUUFROUWSUUFQIMBUUCEUJZJLCUUFRWTXBVMZXCYIUUCROUWRUUCQYHYJUUB
      UGZIMBUUCRWTXBVMZVFVGWIWKXDWIIJKBCUUBYBXEXFUVNXEXGXHUUPUUJUUTUUMUUOUUJUUT
      VSUULUUOUUJUURIBUKZJCUKZUUTUUOUVSUUJUXEUUOUVSYNCSZUVSPZUUJUUOUXFUVSUUOUXF
      UUICSJLCUUFUUIUWTUUIUHXICYNUUIUNUOXJUUJUVFUXGUVGJCYDYNXKXLXMUUOUVEUXDJCUW
      OUWQUVEYOBSZUXDPZUXDUVEUXHUWGPZUWQUXIUVEUVIUXJUVJIBAYOXKWCUWQUWGUXDUXHUWQ
      UVDUURIBUUOYKIUWNUWHUTUWQYIPYPUUCAUXCVNXNXOXPUWQUXHUXDUWQUWAUXHUXAUWAUXHU
      UFBSIMBUUCUUFUXBUUFUHXIBYOUUFUNUOXQXJXRXNXSUURJICBXTXMUPXFVTYAYC $.
      $( [21-Feb-2004] $)
  $}

  ${
    $d x y z w v f g A $.  $d x y z w v f g B $.  $d x y z w v f g C $.
    xpmapen.1 $e |- A e. _V $.
    xpmapen.2 $e |- B e. _V $.
    xpmapen.3 $e |- C e. _V $.
    ${
      $d y D $.  $d y R $.  $d x S $.
      xpmapenlem.4 $e |- D =
                    { <. z , w >. | ( z e. C /\ w = U. dom { ( x ` z ) } ) } $.
      xpmapenlem.5 $e |- R =
                    { <. z , w >. | ( z e. C /\ w = U. ran { ( x ` z ) } ) } $.
      xpmapenlem.6 $e |- S = { <. z , w >. | ( z e. C /\ w =
                       <. ( U. dom { y } ` z ) , ( U. ran { y } ` z ) >. ) } $.
      $( Lemma for ~ xpmapen . $)
      xpmapenlem1 $p |- ( ( y = <. D , R >. -> A. z y = <. D , R >. ) /\
                           ( y = <. D , R >. -> A. w y = <. D , R >. ) ) $=
        ( cv wceq wal hbxfreq cop wi wcel cfv csn cdm cuni wa copab hbopab1 crn
        hbop hbeleq hbopab2 pm3.2i ) BQHIUAZRZUQCSUBUQUQDSUBCBUPCBHICBHCQZGUCZD
        QZURAQUDUEZUFUGRUHZCDUIZNVBCDBUJTCBIUSUTVAUKUGRUHZCDUIZOVDCDBUJTULUMDBU
        PDBHIDBHVCNVBCDBUNTDBIVEOVDCDBUNTULUMUO $.
        $( [2-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem2 $p |- ( ( y = <. D , R >. /\ z e. C ) ->
             ( ( U. dom { y } ` z ) = U. dom { ( x ` z ) } /\
               ( U. ran { y } ` z ) = U. ran { ( x ` z ) } ) ) $=
        ( wceq csn cuni cfv cv cop wcel cdm crn copab opeq1i sneqi syl6eq dmeqd
        wa sneq unieqd opabex2 op1sta fveq1d cvv snex dmex uniex mpan2 sylan9eq
        fvopab2 opeq2i rneqd fopabex2 op2nda rnex jca ) BUAZHIUBZQZCUAZGUCZUKVM
        VJRZUDZSZTZVMAUATZRZUDZSZQVMVOUEZSZTZVTUEZSZQVLVNVRVMVNDUAZWBQUKCDUFZTZ
        WBVLVMVQWIVLVQWIIUBZRZUDZSWIVLVPWMVLVOWLVLVOVKRZWLVJVKULZVKWKHWIINUGUHU
        IUJUMWIICDGWBMUNUOUIUPVNWBUQUCWJWBQWAVTVSURZUSUTCDGWBUQVCVAVBVLVNWEVMVN
        WHWGQUKCDUFZTZWGVLVMWDWQVLWDHWQUBZRZUEZSWQVLWCXAVLVOWTVLVOWNWTWOVKWSIWQ
        HOVDUHUIVEUMHWQCDGWBHMNVFCDGWGMUNVGUIUPVNWGUQUCWRWGQWFVTWPVHUTCDGWGUQVC
        VAVBVI $.
        $( [1-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem3 $p |- ( ( x : C --> ( A X. B ) /\ y = <. D , R >. ) ->
                        x = S ) $=
        ( cv wceq wa wcel cxp cop cfv copab wfn ffn dffn5 sylib adantr csn cuni
        wf cdm crn ax-17 wal wi xpmapenlem1 simpli simpri ffvelrn elxp4 simplbi
        hban adantlr xpmapenlem2 opeq12 adantll eqtr4d pm5.32da opabbid syl6eqr
        syl eqeq2d eqtrd ) GEFUAZAQZULZBQZHIUBRZSZVQCQZGTZDQZWBVQUCZRZSZCDUDZJV
        RVQWHRZVTVRVQGUEWIGVPVQUFCDGVQUGUHUIWAWHWCWDWBVSUJZUMUKUCZWBWJUNUKUCZUB
        ZRZSZCDUDJWAWGWOCDVRVTCVRCUOVTVTCUPUQZVTVTDUPUQZABCDEFGHIJKLMNOPURZUSVD
        VRVTDVRDUOWPWQWRUTVDWAWCWFWNWAWCSZWEWMWDWSWEWEUJZUMUKZWTUNUKZUBZWMVRWCW
        EXCRZVTVRWCSWEVPTZXDGVPWBVQVAXEXDXAETXBFTSWEEFVBVCVMVEVTWCWMXCRZVRVTWCS
        WKXARWLXBRSXFABCDEEGHIJKKMNOPVFWKWLXAXBVGVMVHVIVNVJVKPVLVO $.
        $( [1-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem4 $p |- ( ( ( y = <. U. dom { y } , U. ran { y } >. /\
        ( U. dom { y } : C --> A /\ U. ran { y } : C --> B ) ) /\ x = S ) ->
                     ( x : C --> ( A X. B ) /\ y = <. D , R >. ) ) $=
        ( cuni wceq wa wcel cv csn cdm crn cop wf cxp ffvelrn ralrimiva anim12i
        cfv wral copab eqtr feq1d mpan2 fvex opelxp ralbii eqid fopab2 3bitr3ri
        wb r19.26 syl6rbbr biimpac sylan adantll fopabfv simplbi hbopab1 hbeleq
        hbxfreq hbopab2 fveq1d opex fvopab2 sylan9eq sneqd unieqd op1sta syl6eq
        cvv dmeqd eqeq2d pm5.32da opabbid syl5req adantlr rneqd opeq12d syl5ibr
        op2nda eqeq1 impl jca ) BUAZWQUBZUCQZWRUDQZUEZRZGEWSUFZGFWTUFZSZSAUAZJR
        ZSGEFUGZXFUFZWQHIUEZRZXEXGXIXBXECUAZWSUKZETZCGULZXLWTUKZFTZCGULZSZXGXIX
        CXOXDXRXCXNCGGEXLWSUHUIXDXQCGGFXLWTUHUIUJXGXSXIXGXIGXHXLGTZDUAZXMXPUEZR
        SZCDUMZUFZXSXGJYDRZXIYEVCPXGYFSGXHXFYDXFJYDUNZUOUPYBXHTZCGULXNXQSZCGULY
        EXSYHYICGXMXPEFXLWTUQZURUSCDGXHYBYDYDUTVAXNXQCGVDVBVEVFVGVHXBXEXGXKXEXG
        SZXKXBXAXJRYKWSHWTIXCXGWSHRXDXCXGWSXTYAXMRZSZCDUMZHXCWSYNRXOCDGEWSVIVJX
        GHXTYAXLXFUKZUBZUCZQZRZSZCDUMYNNXGYTYMCDCAJCAJYDPYCCDAVKVMVLZDAJDAJYDPY
        CCDAVNVMVLZXGXTYSYLXGXTSZYRXMYAUUCYRYBUBZUCZQXMUUCYQUUEUUCYPUUDUUCYOYBX
        GXTYOXLYDUKZYBXGXLXFYDXGYFXFYDRPYGUPVOXTYBWCTUUFYBRXMXPVPCDGYBWCVQUPVRV
        SZWDVTXMXPXLWSUQZWAWBWEWFWGWHVRWIXDXGWTIRXCXDXGWTXTYAXPRZSZCDUMZIXDWTUU
        KRXRCDGFWTVIVJXGIXTYAYPUDZQZRZSZCDUMUUKOXGUUOUUJCDUUAUUBXGXTUUNUUIUUCUU
        MXPYAUUCUUMUUDUDZQXPUUCUULUUPUUCYPUUDUUGWJVTXMXPUUHYJWMWBWEWFWGWHVRVHWK
        WQXAXJWNWLWOWP $.
        $( [2-May-2004] $)

      $( Lemma for ~ xpmapen . $)
      xpmapenlem5 $p |- ( ( A X. B ) ^m C ) ~~ ( ( A ^m C ) X. ( B ^m C ) ) $=
        ( cvv wcel wceq wa cxp cmap cop ovex opex a1i csn cdm cuni cfv fopabex2
        co cv crn wf opelvv eleq1 mpbiri adantl elxp4 simplbi copab wral op1sta
        syl sneq unieqd wal xpmapenlem1 simpli simpri xpmapenlem2 simpld eqeq2d
        dmeqd wi pm5.32da opabbid syl6eqr 3eqtr4a op2nda rneqd simprd ralrimiva
        jca ffvelrn adantr ax-17 xpmapenlem3 fveq1d fveq1i fvopab2 mpan2 syl5eq
        hban sylan9eq eleq1d opelxp syl6bb ralbida r19.26 mpbid fopabfv anbi12i
        fvex an4 bitr4i sylib jca31 xpmapenlem4 impbii xpex elmap anbi1i anbi2i
        bitri 3bitr4i en2 ) ABEFUAZGUBULZEGUBULZFGUBULZUAZHIUCZJXSGUBUDYDQRAUMZ
        XTRZHIUEUFJQRBUMZYCRZCDGCUMZYGUGZUHZUIZUJZYIYJUNZUIZUJZUCZJMPUKUFGXSYEU
        OZYGYDSZTZYGYLYOUCSZGEYLUOZGFYOUOZTZTZYEJSZTZYFYSTYHUUFTYTUUGYTUUAUUDUU
        FYTYGQQUAZRZUUAYSUUIYRYSUUIYDUUHRHICDGYIYEUJZUGZUHUIZHMNUKZUPYGYDUUHUQU
        RUSUUIUUAYLQRYOQRTYGQQUTVAVEYTYLYIGRZDUMZYMSZTZCDVBZSZYOUUNUUOYPSZTZCDV
        BZSZTZYMERZCGVCZYPFRZCGVCZTZTZUUDYTUVDUVIYSUVDYRYSUUSUVCYSYDUGZUHZUIHYL
        UURHIUUMVDYSYKUVLYSYJUVKYGYDVFZVOVGYSUURUUNUUOUULSZTZCDVBHYSUUQUVOCDYSY
        SCVHVPZYSYSDVHVPZABCDEFGHIJKLMNOPVIZVJZUVPUVQUVRVKZYSUUNUUPUVNYSUUNTZYM
        UULUUOUWAYMUULSZYPUUKUNUIZSZABCDEFGHIJKLMNOPVLZVMVNVQVRNVSVTYSUVKUNZUII
        YOUVBHIUUMCDGUWCIMOUKWAYSYNUWFYSYJUVKUVMWBVGYSUVBUUNUUOUWCSZTZCDVBIYSUV
        AUWHCDUVSUVTYSUUNUUTUWGUWAYPUWCUUOUWAUWBUWDUWEWCVNVQVROVSVTWEUSYTUUJXSR
        ZCGVCZUVIYRUWJYSYRUWICGGXSYIYEWFWDWGYTUWJUVEUVGTZCGVCUVIYTUWIUWKCGYRYSC
        YRCWHUVSWOYTUUNTZUWIYQXSRUWKUWLUUJYQXSYTUUNUUJYIJUJZYQYTYIYEJABCDEFGHIJ
        KLMNOPWIZWJUUNUWMYIUUNUUOYQSTCDVBZUJZYQYIJUWOPWKUUNYQQRUWPYQSYMYPUECDGY
        QQWLWMWNWPWQYMYPEFYIYOXEWRWSWTUVEUVGCGXAWSXBWEUVJUUSUVFTZUVCUVHTZTUUDUU
        SUVCUVFUVHXFUUBUWQUUCUWRCDGEYLXCCDGFYOXCXDXGXHUWNXIABCDEFGHIJKLMNOPXJXK
        YFYRYSXSGYEEFKLXLMXMXNYHUUEUUFYHUUAYLYARZYOYBRZTZTUUEYGYAYBUTUXAUUDUUAU
        WSUUBUWTUUCEGYLKMXMFGYOLMXMXDXOXPXNXQXR $.
        $( [1-May-2004] $)
    $}

    $( Equinumerosity law for set exponentiation of a cross product.  Exercise
       4.47 of [Mendelson] p. 255. $)
    xpmapen $p |- ( ( A X. B ) ^m C ) ~~ ( ( A ^m C ) X. ( B ^m C ) ) $=
      ( vx vy vz vw cv cfv csn cdm cuni wceq wa copab crn eqid wcel xpmapenlem5
      cop ) GHIJABCIKZCUAZJKZUDGKLMZNOPQIJRZUEUFUGSOPQIJRZUEUFUDHKMZNOLUDUJSOLU
      CPQIJRZDEFUHTUITUKTUB $.
      $( [23-Feb-2004] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    mapunen.1 $e |- A e. _V $.
    mapunen.2 $e |- B e. _V $.
    mapunen.3 $e |- C e. _V $.
    $( Equinumerosity law for set exponentiation of a disjoint union.  Exercise
       4.45 of [Mendelson] p. 255. $)
    mapunen $p |- ( ( A i^i B ) = (/) ->
                  ( C ^m ( A u. B ) ) ~~ ( ( C ^m A ) X. ( C ^m B ) ) ) $=
      ( vx c0 wceq cun cmap cres cint wcel wi wf wa elmap sylbi syl6eq vy co cv
      cin cxp cop csn crn cuni cvv ovex a1i wss ssun1 fssres mpan2 sylibr ssun2
      jca unex vex resex opelxp 3imtr4i elxp5 simprbi anbi12i sylib fun wb feq3
      unidm ax-mp bitr4i sylan expcom wfn anim12i reseq1 resundir fnresdm incom
      ffn uneq1d eqeq1i fnresdisj biimpa sylan2b un0 sylan9eq anassrs sylan9eqr
      uneq2d uncom eqtri an32s opeq12d eqeq2d biimparc exp44 imp sylan2 adantrl
      impcom syl inteq inteqd op1stb sneq rneqd unieqd uneq12d resundi syl6reqr
      op2nda sylan9req ex ad2antrl impbid en3d ) ABUDZHIZGUACABJZKUBZCAKUBZCBKU
      BZUEZGUCZALZYHBLZUFZUAUCZMZMZYLUGZUHZUIZJZYDUJNYBCYCKUKULYHYDNZYKYGNZOYBY
      CCYHPZYIYENZYJYFNZQYSYTUUAUUBUUCUUAACYIPZUUBUUAAYCUMUUDABUNYCCAYHUOUPCAYI
      FDRUQUUABCYJPZUUCUUABYCUMUUEBAURYCCBYHUOUPCBYJFERUQUSCYCYHFABDEUTZRZYIYJY
      EYFYHBGVAZVBZVCVDULYLYGNZYBYRYDNZUUJACYNPZBCYQPZQZYBUUKUUJYNYENZYQYFNZQZU
      UNUUJYLYNYQUFZIZUUQYLYEYFVEZVFUUOUULUUPUUMCAYNFDRZCBYQFERZVGVHUUNYBQYCCCJ
      ZYRPZUUKABCCYNYQVIUUKYCCYRPZUVDCYCYRFUUFRUVCCIUVDUVEVJCVLUVCCYCYRVKVMVNUQ
      VOVPYBYSUUJQZYHYRIZYLYKIZVJYBUVFQUVGUVHYBUUJUVGUVHOZYSUUJYBUVIUUJUUSUUQQY
      BUVIOZUUTUUQUUSYNAVQZYQBVQZQZUVJUUOUVKUUPUVLUUOUULUVKUVAACYNWCSUUPUUMUVLU
      VBBCYQWCSVRUUSUVMUVJUUSUVMYBUVGUVHUVMYBQZUVGQZUVHUUSUVOYKUURYLUVOYIYNYJYQ
      UVGUVNYIYNALZYQALZJZYNUVGYIYRALUVRYHYRAVSYNYQAVTTUVKUVLYBUVRYNIUVKUVLYBQZ
      UVRYNUVQJZYNUVKUVPYNUVQAYNWAWDUVSUVTYNHJYNUVSUVQHYNYBUVLBAUDZHIZUVQHIZYAU
      WAHABWBWEUVLUWBUWCBAYQWFWGWHWMYNWITWJWKWLUVGUVNYJYNBLZYQBLZJZYQUVGYJYRBLU
      WFYHYRBVSYNYQBVTTUVKYBUVLUWFYQIUVLUVKYBQZUWFUWDYQJZYQUVLUWEYQUWDBYQWAWMUW
      GUWHHYQJZYQUWGUWDHYQUVKYBUWDHIABYNWFWGWDUWIYQHJYQHYQWNYQWIWOTWLWPWLWQWRWS
      WTXAXBSXDXCYSUVHUVGOYBUUJYSUVHUVGYSUVHYHYHYCLZYRYSYHYCVQZUWJYHIYSUUAUWKUU
      GYCCYHWCSYCYHWAXEUVHYRYIYJJUWJUVHYNYIYQYJUVHYNYKMZMYIUVHYMUWLYLYKXFXGYIYJ
      YHAUUHVBZXHTUVHYQYKUGZUHZUIYJUVHYPUWOUVHYOUWNYLYKXIXJXKYIYJUWMUUIXOTXLYHA
      BXMXNXPXQXRXSXQXT $.
      $( [23-Sep-2004] $)
  $}

  ${
    $d x y A $.  $d x B $.
    pwen.1 $e |- B e. _V $.
    $( If two sets are equinumerous, then their power sets are equinumerous.
       Proposition 10.15 of [TakeutiZaring] p. 87. $)
    pwen $p |- ( A ~~ B -> ~P A ~~ ~P B ) $=
      ( vx cvv wcel cen wbr cpw relen wi c2o cmap co con0 2on ovex pw2en mp2an
      wb brrelexi cv wceq breq1 pweq breq1d imbi12d enrefg ax-mp elexi mpan vex
      mapen enen1 enen2 bitr2i sylib vtoclg mpcom ) AEFABGHZAIZBIZGHZABGJUADUBZ
      BGHZVDIZVBGHZKUTVCKDAEVDAUCZVEUTVGVCVDABGUDVHVFVAVBGVDAUEUFUGVELVDMNZLBMN
      ZGHZVGLLGHZVEVKLOFVLPLOUHUILLVDBLOPUJCUMUKVGVIVBGHZVKVIEFVFVIGHVGVMTLVDMQ
      VDDULRVFVIVBEUNSVJEFVBVJGHVMVKTLBMQBCRVBVJVIEUOSUPUQURUS $.
      $( [29-Mar-2007] $) $( [29-Jan-2004] $)
  $}

  ${
    $d x y z f A $.  $d x y z f B $.  $d x y z f C $.
    ssenen.1 $e |- A e. _V $.
    ssenen.2 $e |- B e. _V $.
    $( Equinumerosity of equinumerous subsets of a set. $)
    ssenen $p |- ( A ~~ B ->
             { x | ( x C_ A /\ x ~~ C ) } ~~ { x | ( x C_ B /\ x ~~ C ) } ) $=
      ( vf cen wbr cv cab cin wss wa cima cvv wcel sylan syl wceq vy vz cpw wex
      wf1o bren ccnv pwex inex1 a1i wf1 f1of1 vex f1imaen entr expl wfo imassrn
      f1ofo crn forn syl5sseq jctild elin elpw2 breq1 elab anbi12i bitri imaexg
      ax-mp 3imtr4g wi f1ocnv cnvex simpl sylib sylbi imaeq2 wrel f1orel dfrel2
      wb imaeq1d adantr f1imacnv eqtr3d sylan9eqr eqcomd sylan2 adantrl adantrr
      ex impbid en3d exlimiv df-pw ineq1i inab eqtri 3brtr3g ) BCHIZBUCZAJZDHIZ
      AKZLZCUCZXFLZXDBMZXENAKZXDCMZXENAKZHXBBCGJZUEZGUDXGXIHIZBCGFUFXOXPGXOUAUB
      XGXIXNUAJZOZXNUGZUBJZOZXGPQXOXCXFBEUHUIUJXOXQBMZXQDHIZNZXRCMZXRDHIZNZXQXG
      QZXRXIQZXOYDYFYEXOYBYCYFXOYBNZXRXQHIZYCYFXOBCXNUKZYBYKBCXNULZBCXQXNUAUMZU
      NRXRXQDUORUPXOBCXNUQZYEBCXNUSYOXNUTXRCXNXQURBCXNVAVBSVCYHXQXCQZXQXFQZNZYD
      XQXCXFVDZYPYBYQYCXQBEVEZXEYCAXQYNXDXQDHVFVGVHVIYIXRXHQZXRXFQZNYGXRXHXFVDU
      UAYEUUBYFXRCFVEXEYFAXRXNPQXRPQGUMZXNXQPVJVKXDXRDHVFVGVHVIVLXOXTCMZXTDHIZN
      ZYABMZYADHIZNZXTXIQZYAXGQZXOCBXSUEZUUFUUIVMBCXNVNZUULUUFUUHUUGUULUUDUUEUU
      HUULUUDNYAXTHIZUUEUUHUULCBXSUKZUUDUUNCBXSULZCBXTXSUBUMZUNRYAXTDUORUPUULCB
      XSUQZUUGCBXSUSUURXSUTYABXSXTURCBXSVAVBSVCSUUJXTXHQZXTXFQZNZUUFXTXHXFVDZUU
      SUUDUUTUUEXTCFVEZXEUUEAXTUUQXDXTDHVFVGVHVIUUKYAXCQZYAXFQZNUUIYAXCXFVDUVDU
      UGUVEUUHYABEVEXEUUHAYAXSPQYAPQXNUUCVOXSXTPVJVKXDYADHVFVGVHVIVLXOYHUUJNZXQ
      YATZXTXRTZWCXOUVFNUVGUVHXOUUJUVGUVHVMZYHUUJXOUUDUVIUUJUVAUUDUVBUVAUUSUUDU
      USUUTVPUVCVQVRXOUUDNZUVGUVHUVJUVGNXRXTUVGUVJXRXNYAOZXTXQYAXNVSUVJXSUGZYAO
      ZUVKXTXOUVMUVKTUUDXOUVLXNYAXOXNVTUVLXNTBCXNWAXNWBVQWDWEXOUUOUUDUVMXTTXOUU
      LUUOUUMUUPSCBXTXSWFRWGWHWIWMWJWKXOYHUVHUVGVMZUUJYHXOYBUVNYHYRYBYSYRYPYBYP
      YQVPYTVQVRYJUVHUVGYJUVHNYAXQUVHYJYAXSXROZXQXTXRXSVSXOYLYBUVOXQTYMBCXQXNWF
      RWHWIWMWJWLWNWMWOWPVRXGXJAKZXFLXKXCUVPXFABWQWRXJXEAWSWTXIXLAKZXFLXMXHUVQX
      FACWQWRXLXEAWSWTXA $.
      $( [21-Mar-2007] $) $( [30-Sep-2004] $)
  $}

  ${
    $d x y A $.
    limenpsi.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to a proper subset of itself. $)
    limenpsi $p |- ( A e. V -> A ~~ ( A \ { (/) } ) ) $=
      ( vx vy wcel c0 csn cdif cdom wbr cv csuc wb ax-mp con0 wceq ordelon mpan
      cen wne wlim limsuc biimpi nsuceq0 jctir eldifsn sylibr word limord suc11
      wa syl2an dom2 wss difss ssdom2g mpi sbth syl2anc ) ABFZAAGHZIZJKVCAJKZAV
      CTKDEAVCDLZMZELZMZBVEAFZVFAFZVFGUAZULVFVCFVIVJVKVIVJAUBZVIVJNCAVEUCOUDVEU
      EUFVFAGUGUHVIVEPFZVGPFZVFVHQVEVGQNVGAFZAUIZVIVMVLVPCAUJOZAVERSVPVOVNVQAVG
      RSVEVGUKUMUNVAVCAUOVDAVBUPVCABUQURAVCUSUT $.
      $( [30-Oct-2003] $)
  $}

  ${
    limensuci.1 $e |- Lim A $.
    $( A limit ordinal is equinumerous to its successor. $)
    limensuci $p |- ( A e. V -> A ~~ suc A ) $=
      ( wcel csn cdif cun csuc cen wbr cvv difexg limenpsi ensymg 0ex cin ax-mp
      c0 wceq mpbi sylc en2sn mpan wa incom disjdif eqtri wn word limord ordirr
      wlim disjsn mpbir unen mpanr12 syl2anc wss 0ellim snss undif uncom eqtr3i
      df-suc 3brtr4g ) ABDZAREZFZVGGZAAEZGZAAHIVFVHAIJZVGVJIJZVIVKIJZVFVHKDAVHI
      JVLAVGBLABCMAVHKNUARKDVFVMORAKBUBUCVLVMUDVHVGPZRSAVJPRSZVNVOVGVHPRVHVGUEV
      GAUFUGVPAADUHZAUIZVQAULZVRCAUJQAUKQAAUMUNVHAVGVJUOUPUQVGVHGZAVIVGAURZVTAS
      RADZWAVSWBCAUSQRAOUTTVGAVATVGVHVBVCAVDVE $.
      $( [30-Oct-2003] $)
  $}

  $( A limit ordinal is equinumerous to its successor. $)
  limensuc $p |- ( ( A e. V /\ Lim A ) -> A ~~ suc A ) $=
    ( wlim wcel csuc cen wbr wi con0 cif wceq eleq1 suceq breq12d imbi12d limeq
    id limon elimhyp limensuci dedth impcom ) ACZABDZAAEZFGZUCUDUFHUCAIJZBDZUGU
    GEZFGZHAIAUGKZUDUHUFUJAUGBLUKAUGUEUIFUKQAUGMNOUGBUCUGCICAIAUGPIUGPRSTUAUB
    $.
    $( [30-Oct-2003] $)

  ${
    $d x y A $.
    $( Any infinite ordinal is equinumerous to its successor.  Exercise 7 of
       [TakeutiZaring] p. 88.  Proved without the Axiom of Infinity.  (Revised
       by Mario Carneiro, 13-Jan-2013.) $)
    infensuc $p |- ( ( A e. On /\ om C_ A ) -> A ~~ suc A ) $=
      ( vx vy con0 wcel com wss wa csuc cen wbr wceq cvv wi cv id suceq breq12d
      csn vex onprc eleq1 mtbiri ssexg nsyl3 omon ori nsyl2 weq limom limensuci
      ancoms sucex en2sn mp2an cin c0 wel wn eloni ordirr disjsn sylibr sucelon
      word syl 3imtr4i jca cun unen df-suc 3brtr4g ex syl5 mpan2 com12 ad2antrr
      wlim wral limensuc mpan a1d tfindsg exp31 com23 imp mpd ) ADEZFAGZHZFDEZA
      AIZJKZWJFDLZWKWNFMEZWJWNWODMEUAFDMUBUCWIWHWOFADUDULUEWKWNUFUGUHWHWIWKWMNW
      HWKWIWMWHWKWIWMBOZWPIZJKZFFIZJKCOZWTIZJKZXAXAIZJKZWMBCAFWPFLZWPFWQWSJXEPW
      PFQRBCUIZWPWTWQXAJXFPWPWTQRWPXALZWPXAWQXCJXGPWPXAQRWPALZWPAWQWLJXHPWPAQRF
      DUJUKWTDEZXBXDNWKFWTGZXBXIXDXBWTSZXASZJKZXIXDNWTMEXAMEXMCTZWTXNUMWTXAMMUN
      UOXIWTXKUPUQLZXAXLUPUQLZHZXBXMHZXDXIXOXPXICCURUSZXOXIWTVEXSWTUTWTVAVFWTWT
      VBVCXADEZXAXAEUSZXIXPXTXAVEYAXAUTXAVAVFWTVDXAXAVBVGVHXRXQXDXRXQHWTXKVIXAX
      LVIXAXCJWTXAXKXLVJWTVKXAVKVLVMVNVOVPVQWPVRZWKHFWPGZHWRXJXBNCWPVSYBWRWKYCW
      PMEYBWRBTWPMVTWAVQWBWCWDWEWFWG $.
      $( [13-Jan-2013] $) $( [30-Oct-2003] $)
  $}

