$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                      Ordinal isomorphism, Hartog's theorem
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    $d f w x y z A $.  $d f w x y z B $.
    $( Order-isomorphic ordinal numbers are equal.  (Contributed by Jeff
       Hankins, 16-Oct-2009.) $)
    ordiso $p |- ( ( A e. On /\ B e. On ) -> ( A = B
                <-> E. f f Isom _E , _E ( A , B ) ) ) $=
      ( vx vy vz vw con0 wcel wa wceq cep wbr cfv wral wn ad2antrr weq adantlr
      wi cv wiso wex w3a cid cres resiexg 3ad2ant1 wf1o wb f1oi f1oeq3 3ad2ant3
      fvresi ad2antrl ad2antll breq12d bicomd ralrimivva df-iso sylanbrc isoeq1
      cvv mpbii cla4egv sylc 3expia wrex wel crn simpllr simpr wf f1of ad2antlr
      isof1o syl simplrr ffvelrn syl2anc jca ontr1 wfo f1ofo forn 3syl eleqtrrd
      wfn f1ofn fvelrnb mpbid simprrr simprl eqeltrd fvex sylibr simplr simprrl
      epelc isorel syl12anc mpbird epel sylib simprr eleq1 fveq2 eqeq12d rcla4v
      id imbi12d exp3a com12 pm2.27 syl8 com24 syld com14 imp42 adantllr eqtr3d
      adantrl expr mpd rexlimdv imp44 biimpri ancomsd adantrll simprlr eqeltrrd
      ex imp impbid eqrdv exp43 iman ralbii bitri adantll imbi12i imnan xchbinx
      ralrimiv ancom ralnex rexnal onelon adantrd ancrd reximdv2 notbid anbi12d
      onminex syl6 syl5bir mt3d rcla4cva sylan adantlrr eleq2d bitr3d a1i simpl
      adantl syldd com23 impr sylbid exlimdv ) AHIZBHIZJZABKZABLLCUAZUBZCUCZUVK
      UVLUVNUVQUVKUVLUVNUDZUEAUFZVCIZABLLUVSUBZUVQUVKUVLUVTUVNAHUGUHUVRABUVSUIZ
      DUAZEUAZLMZUWCUVSNZUWDUVSNZLMZUJZEAODAOUWAUVRAAUVSUIZUWBAUKUVNUVKUWJUWBUJ
      UVLABAUVSULUMVDUVRUWIDEAAUVRUWCAIZUWDAIZJJZUWHUWEUWMUWFUWCUWGUWDLUWKUWFUW
      CKUVRUWLAUWCUNUOUWLUWGUWDKUVRUWKAUWDUNUPUQURUSDEABLLUVSUTVAUVPUWACUVSVCAB
      LLUVSUVOVBVEVFVGUVMUVPUVNCUVMUVPUVNUVMUVPJZUWCUVONZUWCKZDAOZUVNUWNUWQUWKU
      WPPZJZUWLUWDUVONZUWDKZPZJZPZEUWCOZJZDHVHZUWNUWLUXATZEUWCOZUWKUWPTZTZDHOZU
      XGPZUWNUXKDHUWNUWCHIZUXIUWKUWPUWNUXNJZUXIUWKJZJZFUWOUWCUXQFUAZUWOIZFDVIZU
      XQUXSUXTUXQUXSJZGUAZUVONZUXRKZGAVHZUXTUYAUXRUVOVJZIZUYEUYAUXRBUYFUYAUVLUX
      SUWOBIZJUXRBIZUXOUVLUXPUXSUVKUVLUVPUXNVKQUYAUXSUYHUXQUXSVLUYAABUVOVMZUWKU
      YHUXOUYJUXPUXSUVPUYJUVMUXNUVPABUVOUIZUYJABLLUVOVPZABUVOVNVQZVOQUXOUXIUWKU
      XSVRABUWCUVOVSVTWAUXRUWOBWBVFUXOUYFBKZUXPUXSUVPUYNUVMUXNUVPUYKABUVOWCUYNU
      YLABUVOWDABUVOWEWFZVOQWGUXOUYGUYEUJZUXPUXSUVPUYPUVMUXNUVPUYKUVOAWHUYPUYLA
      BUVOWIGAUXRUVOWJWFZVOQWKUYAUYDUXTGAUYAUYBAIZUYDUXTUXQUXSUYRUYDJZUXTUXQUXS
      UYSJZJZGDVIZUXTVUAUYBUWCLMZVUBVUAVUCUYCUWOLMZVUAUYCUWOIVUDVUAUYCUXRUWOUXQ
      UXSUYRUYDWLUXQUXSUYSWMWNUYCUWOUYBUVOWOUWCUVOWOZWSWPVUAUVPUYRUWKVUCVUDUJUX
      OUVPUXPUYTUVMUVPUXNWQQUXQUXSUYRUYDWRUXOUXIUWKUYTVRABUYBUWCLLUVOWTXAXBGDXC
      XDUXQUYTVUBUXTUXQUYTVUBJJZUXRUYBUWCVUFUYCUXRUYBUYTUYDUXQVUBUXSUYRUYDXEUOU
      XQVUBUYCUYBKZUYTUWNUXPVUBVUGUXNUVMUXPVUBVUGUVPUVKUXPVUBVUGUVLUVKUXIUWKVUB
      VUGVUBUXIUWKUVKVUGVUBUXIUYRVUGTZUWKUVKVUGTTUXHVUHEUYBUWCEGRZUWLUYRUXAVUGU
      WDUYBAXFVUIUWTUYCUWDUYBUWDUYBUVOXGVUIXJXHXKXIVUBUVKUWKVUHVUGVUBUVKUWKUYRV
      UHVUGTUVKVUBUWKUYRTUVKVUBUWKUYRUYBUWCAWBXLXMUYRVUGXNXOXPXQXRXSXTXTXTYBYAU
      XQUYTVUBXEWNYCYDYCXLYEYDYLUXOUXPUXTUXSUXOUXPUXTJZJZUXRUVONZUXRUWOUWNVUJVU
      LUXRKZUXNUVMVUJVUMUVPUVKVUJVUMUVLUVKUXIUWKUXTVUMUXTUXIUWKUVKVUMUXTUXIUXRA
      IZVUMTZUWKUVKVUMTTUXHVUOEUXRUWCEFRZUWLVUNUXAVUMUWDUXRAXFVUPUWTVULUWDUXRUW
      DUXRUVOXGVUPXJXHXKXIUXTUVKUWKVUOVUMUXTUVKUWKVUNVUOVUMTUVKUXTUWKVUNTUVKUXT
      UWKVUNUXRUWCAWBZXLXMVUNVUMXNXOXPXQXRYFSSSVUKVULUWOLMZVULUWOIVUKUXRUWCLMZV
      URUXTVUSUXOUXPVUSUXTFDXCYGUPVUKUVPVUNUWKVUSVURUJUVMUVPUXNVUJVKUXOUWKUXTVU
      NUXIUWNUWKUXTJZVUNUXNUVMVUTVUNUVPUVKVUTVUNUVLUVKVUTVUNUVKUXTUWKVUNVUQYHYM
      SSSYIUXOUXIUWKUXTYJABUXRUWCLLUVOWTXAWKVULUWOUXRUVOWOVUEWSXDYKYCYNYOYPUUDU
      XLUXFPZDHOUXMUXKVVADHUXKUXEUWSJZUXFUXKUXEUWSPZTVVBPUXIUXEUXJVVCUXHUXDEUWC
      UWLUXAYQYRUWKUWPYQUUAUXEUWSUUBYSUXEUWSUUEUUCYRUXFDHUUFYSXDUWQPUWRDAVHZUWN
      UXGUWPDAUUGUWNVVDUWSDHVHUXGUWNUWRUWSDAHUWNUWSUXNUVKUWSUXNTUVLUVPUVKUWKUXN
      UWRUVKUWKUXNAUWCUUHYLUUIQUUJUUKUWSUXCDEDERZUWKUWLUWRUXBUWCUWDAXFVVEUWPUXA
      VVEUWOUWTUWCUWDUWCUWDUVOXGVVEXJXHUULUUMUUNUUOUUPUUQUVMUVPUWQUVNUVMUVPUWQJ
      ZJZFABVVGVUNUYIVVGVUNUYIVVGVUNJVULUXRBVVFVUNVUMUVMUWQVUNVUMUVPUWPVUMDUXRA
      DFRZUWOVULUWCUXRUWCUXRUVOXGVVHXJXHUURYTYTUVMUVPVUNVULBIZUWQUVPVUNVVIUVMUV
      PUYJVUNVVIUYMABUXRUVOVSUUSYTUUTYKYLVVGUYIUYEVUNVVGUYGUYIUYEVVGUYFBUXRUVPU
      YNUVMUWQUYOUOUVAUVPUYPUVMUWQUYQUOUVBVVGUYDVUNGAUVMUVPUWQUYRUYDVUNTZTUWNUY
      RUWQVVJUWNUYRUWQVUGVVJUYRUWQVUGTTUWNUWPVUGDUYBADGRZUWOUYCUWCUYBUWCUYBUVOX
      GVVKXJXHXIUVCUWNUYRVUGUYDVUNUWNUYRJZVUGUYDJZJUXRUYBAVVMFGRVVLVVMUYCUXRUYB
      VUGUYDVLVUGUYDUVDYAUVEUWNUYRVVMWQWNYPUVFUVGUVHYEUVIYNYOYCYDYLUVJYN $.
      $( [16-Oct-2009] $)
  $}

  ${
    $d a b c f g h j k m n o p q r s t u v w x y z A $.
    $d a b c f h m n o p q x y z B $.  $d c m n o p q u C $.
    $d h j m n o p q r u v w y z D $.
    $d b c h j m n o p q r s u v w x y z F $.  $d b c h m n o p q G $.
    $d a f g h j m n o p q r s u v w x z H $.
    $d a b c f g h j k m n o p q r s t u v w x y z R $.
    ${
      ordtypelem.1 $e |- A e. _V $.
      ordtypelem.2 $e |- B = { h | E. b e. On ( h Fn b /\ A. c e. b ( h ` c ) =
      ( G ` ( h |` c ) ) ) } $.
      ordtypelem.3 $e |- F = U. B $.
      ordtypelem.4 $e |- C = { w e. A | A. j e. ran h j R w } $.
      ordtypelem.5 $e |- D = { w e. A | A. j e. ( F " x ) j R w } $.
      ordtypelem.6 $e |- G = { <. h , c >. | c = U. { v e. C | A. u e. C -. u R
      v } } $.
      ordtypelem.7 $e |- H = { w e. A | A. j e. ( F " y ) j R w } $.
      $( Lemma for ~ ordtype . $)
      ordtypelem1 $p |- ( ( x e. On /\ R We A /\ D =/= (/) ) -> ( F ` x ) e. D
      ) $=
        ( cv con0 wcel wwe c0 wne cfv wa wbr wn wral crab cuni wss cima eqsstri
        ssrab2 ssexi wereucl mp3an2 cres tfr2 wceq copab fveq1i cvv tfrlem7 vex
        wfun resfunexg mp2an rabex uniex cab crn df-ima syl6eqr raleqdv rabbidv
        3eqtr4g eleq2d anbi12d abbidv df-rab unieqd fvopab eqtri syl6eq syl5ibr
        rneq eleq1d 3impib ) AUEZUFUGZFJUHZIUIUJZWQMUKZIUGZWSWTULXBWREUEDUEZJUM
        UNZEIUOZDIUPZUQZIUGZWSIFURWTXHILUECUEJUMZLMWQUSZUOZCFUPZFUBXKCFVAUTZDEF
        IJIFRXMVBZVCVDWRXAXGIWRXAMWQVEZNUKZXGPQAGKMNSTVFXPXOQUEXDEHUOZDHUPZUQZV
        GKQVHZUKXGXONXTUCVIKQXOXSXGMVMWQVJUGXOVJUGPQGKMNSTVKAVLMWQVJVNVOXFXEDIX
        NVPVQKUEZXOVGZXRXFYBXCHUGZXQULZDVRXCIUGZXEULZDVRXRXFYBYDYFDYBYCYEXQXEYB
        HIXCYBXILYAVSZUOZCFUPXLHIYBYHXKCFYBXILYGXJYBYGXOVSXJYAXOWNMWQVTWAWBWCUA
        UBWDZWEYBXDEHIYIWBWFWGXQDHWHXEDIWHWDWIWJWKWLWOWMWP $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype . $)
      ordtypelem2 $p |- ( ( x e. On /\ R We A /\ D =/= (/) )
              -> ( y e. x -> ( F ` y ) R ( F ` x ) ) ) $=
        ( cv con0 wcel wwe c0 wne w3a cfv cima wbr wi wfun cdm wss tfrlem7 onss
        wfn wceq tfr1 fndm ax-mp syl6sseqr funfvima2 3ad2ant1 ordtypelem1 breq2
        sylancr wral ralbidv elrab2 simprbi breq1 rcla4cv 3syl syld ) AUEZUFUGZ
        FJUHZIUIUJZUKZBUEZVTUGZWEMULZMVTUMZUGZWGVTMULZJUNZWAWBWFWIUOZWCWAMUPVTM
        UQZURWLPQGKMNSTUSWAVTUFWMVTUTMUFVAWMUFVBPQGKMNSTVCUFMVDVEVFVTWEMVGVKVHW
        DWJIUGZLUEZWJJUNZLWHVLZWIWKUOABCDEFGHIJKLMNOPQRSTUAUBUCUDVIWNWJFUGWQWOC
        UEZJUNZLWHVLWQCWJFIWRWJVBWSWPLWHWRWJWOJVJVMUBVNVOWPWKLWGWHWOWGWJJVPVQVR
        VS $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype . $)
      ordtypelem3 $p |- ( ( x e. On /\ R We A /\ D =/= (/) )
            -> ( y e. x -> -. ( F ` x ) = ( F ` y ) ) ) $=
        ( cv con0 wcel wwe c0 wne w3a cfv wbr wceq wn ordtypelem2 wpo weso sopo
        wi wor syl 3ad2ant2 cima wral crab ssrab2 eqsstri ordtypelem1 sseldi wa
        breq1 biimprcd poirr nsyli com12 syl2anc syld ) AUEZUFUGZFJUHZIUIUJZUKZ
        BUEZVSUGWDMULZVSMULZJUMZWFWEUNZUOZABCDEFGHIJKLMNOPQRSTUAUBUCUDUPWCFJUQZ
        WFFUGZWGWIUTWAVTWJWBWAFJVAWJFJURFJUSVBVCWCIFWFILUECUEJUMLMVSVDVEZCFVFFU
        BWLCFVGVHABCDEFGHIJKLMNOPQRSTUAUBUCUDVIVJWGWJWKVKZWIWGWHWFWFJUMZWMWHWNW
        GWFWEWFJVLVMFWFJVNVOVPVQVR $.
        $( [17-Oct-2009] $)

      $( Lemma for ~ ordtype .  There is a smallest ordinal number whose image
         has no upper bounds in ` A ` . $)
      ordtypelem4 $p |- ( R We A
            -> E. x e. On ( D = (/) /\ A. y e. x H =/= (/) ) ) $=
        ( vr wwe c0 wceq con0 wrex wne cv wral wa crn cvv wcel wn pm3.24 wi wal
        df-ne ralbii df-ral ralnex 3bitr3i wss cfv wfn tfr1 fvelrnb ax-mp ax-17
        wb hba1 hban wbr cima ssrab2 eqsstri ordtypelem1 3expb sseldi syl5ibcom
        crab eleq1 exp32 com12 a2d a4sd imp rexlimd syl5bi ssrdv ssex ccnv wfun
        syl ordtypelem3 3com12 3exp imp4a alrimdv alimdv r2al syl6ibr cres ssid
        ex tz7.48lem mpan wrel cdm tfrlem6 eqimssi relssres mp2an cnveqi funeqi
        fndm sylib syl6 onprc funrnex df-rn eleq1i dfdm4 eqtr3i 3imtr4g syl5bir
        mtoi jcad mt3i imaeq2 raleqdv rabbidv 3eqtr4g eqeq1d onminex anbi2i
        rexbii sylibr ) FJUFZIUGUHZAUIUJZUUDOUGUKZBAULZUMZUNZAUIUJZUUCUUEMUOZUP
        UQZUULURZUNZUULUSUUEURZUUGUIUQZIUGUKZUTZAVAZUUCUUNUUQAUIUMUUDURZAUIUMUU
        SUUOUUQUUTAUIIUGVBVCUUQAUIVDUUDAUIVEVFUUCUUSUULUUMUUCUUSUULUUCUUSUNZUUK
        FVGUULUVAUEUUKFUEULZUUKUQZUUGMVHZUVBUHZAUIUJZUVAUVBFUQZMUIVIZUVCUVFVNPQ
        GKMNSTVJZAUIUVBMVKVLUVAUVEUVGAUIUUCUUSAUUCAVMUURAVOVPUVGAVMUUCUUSUUPUVE
        UVGUTZUTZUUCUURUVKAUUCUUPUUQUVJUUPUUCUUQUVJUTUUPUUCUUQUVJUUPUUCUUQUNUNZ
        UVDFUQUVEUVGUVLIFUVDILULCULJVQZLMUUGVRZUMZCFWEZFUBUVOCFVSVTUUPUUCUUQUVD
        IUQABCDEFGHIJKLMNOPQRSTUAUBUCUDWAWBWCUVDUVBFWFWDWGWHWIWJWKWLWMWNUUKFRWO
        WRXIUUCUUSMWPZWQZUUMUUCUUSUVDBULZMVHUHURZBUUGUMAUIUMZUVRUUCUUSUUPUVSUUG
        UQZUNUVTUTZBVAZAVAUWAUUCUURUWDAUUCUURUWCBUUCUURUUPUWBUVTUUCUUPUUQUWBUVT
        UTZUUCUUPUUQUWEUUPUUCUUQUWEABCDEFGHIJKLMNOPQRSTUAUBUCUDWSWTXAWIXBXCXDUV
        TABUIUUGXEXFUWAMUIXGZWPZWQZUVRUIUIVGUWAUWHUIXHABUIMUVIXJXKUWGUVQUWFMMXL
        MXMZUIVGUWFMUHPQGKMNSTXNUWIUIUVHUWIUIUHUVIUIMXTVLZXOMUIXPXQXRXSYAYBUVRU
        ULUIUPUQZYCUVRUVQXMZUPUQZUVQUOZUPUQZUULUWKUWMUVRUWOUPUVQYDWHUUKUWLUPMYE
        YFUIUWNUPUWIUIUWNUWJMYGYHYFYIYKYBYLYJYMUUEUUDOUGUHZURZBUUGUMZUNZAUIUJUU
        JUUDUWPABUUGUVSUHZIOUGUWTUVPUVMLMUVSVRZUMZCFWEIOUWTUVOUXBCFUWTUVMLUVNUX
        AUUGUVSMYNYOYPUBUDYQYRYSUUIUWSAUIUUHUWRUUDUUFUWQBUUGOUGVBVCYTUUAUUBWR
        $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype .  Establish injectivity of ` F ` when restricted
         to the ordinal number of ~ ordtypelem4 . $)
      ordtypelem5 $p |- ( ( ( R We A /\ x e. On ) /\ ( D = (/)
            /\ A. y e. x H =/= (/) ) ) -> Fun `' ( F |` x ) ) $=
        ( vz vr vm wwe cv con0 wcel wa c0 wceq wne wral wss cres ccnv wfun onss
        cfv wn ad2antlr csb ax-17 hbcsb1 hbne csbeq1a neeq1d rcla4 cvv vex cima
        wbr crab imaeq2 raleqdv rabbidv 3eqtr4g cbvcsbv ax-mp neeq1i onelon w3a
        wal ordtypelem3 gen2 hb3an hbim hbal eleq1 3anbi13d eleq2 eqeq1d notbid
        fveq2 imbi12d albidv cla4gf eqeq2d imbi2d a4v mp2b 3expia ancoms sylan2
        wi expr com3l syld com13 imp4b adantrl ralrimivv tfr1 tz7.48lem syl2anc
        syl5bi ) FJUHZAUIZUJUKZULZIUMUNZOUMUOZBYAUPZULZULZYAUJUQZUEUIZMVBZUFUIZ
        MVBZUNZVCZUFYJUPUEYAUPMYAURUSUTYBYIXTYGYAVAVDYHYOUEUFYAYJYCYFYJYAUKZYLY
        JUKZULYOXHYDYCYFYPYQYOYPYFYCYQYOXHZYPYFBYJOVEZUMUOZYCYRXHZYEYTBYJYABUGY
        SUMBUGYJOUGUIZYJUKZBVFVGUUBUMUKZBVFVHBUIZYJUNOYSUMBYJOVIVJVKYTAYJIVEZUM
        UOZYPUUAYSUUFUMYJVLUKZYSUUFUNUEVMZBAYJOIVLUUEYAUNZLUICUIJVOZLMUUEVNZUPZ
        CFVPUUKLMYAVNZUPZCFVPOIUUJUUMUUOCFUUJUUKLUULUUNUUEYAMVQVRVSUDUBVTWAWBWC
        YCYPUUGYRXTYBYPUUGYRXHZYBYPULXTYJUJUKZUUPYAYJWDUUQXTUUPUUQXTUUGYRYBXTIU
        MUOZWEZUUEYAUKZYAMVBZUUEMVBZUNZVCZXHZXHZBWFZAWFZUUQXTUUGWEZUUEYJUKZYKUV
        BUNZVCZXHZXHZBWFZUVIYRXHZUVFABABCDEFGHIJKLMNOPQRSTUAUBUCUDWGWHUUHUVHUVO
        XHUUIUVGUVOAUGYJVLUUCAVFZUVNABUVIUVMAUUQXTUUGAUUQAVFXTAVFAUGUUFUMAUGYJI
        UVQVGUUDAVFVHWIUVMAVFWJWKYAYJUNZUVFUVNBUVRUUSUVIUVEUVMUVRYBUUQUURUUGXTY
        AYJUJWLUVRIUUFUMAYJIVIVJWMUVRUUTUVJUVDUVLYAYJUUEWNUVRUVCUVKUVRUVAYKUVBY
        AYJMWQWOWPWRWRWSWTWBUVNUVPBUFUUEYLUNZUVMYRUVIUVSUVJYQUVLYOUUEYLYJWLUVSU
        VKYNUVSUVBYMYKUUEYLMWQXAWPWRXBXCXDXEXFXGXIXJXSXKXLXMXNXOUEUFYAMPQGKMNST
        XPXQXR $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype .  If ` z ` is not in ` ( F " x ) ` , ` m ` is in
         ` ( F " x ) ` , and every element of ` ( F " x ) ` which is less than
         ` m ` is also less than ` z ` , ` m ` is also less than ` z ` . $)
      ordtypelem6 $p |- ( ( ( ( R We A /\ x e. On ) /\ ( ( F " x ) C_ A
              /\ A. y e. x H =/= (/) ) ) /\ ( ( z e. A /\ -. z e. ( F " x ) )
              /\ m e. ( F " x ) ) )
            -> ( A. n ( n R m -> ( n e. ( F " x ) -> n R z ) ) -> m R z ) ) $=
        ( vr wwe cv con0 wcel wa cima wss c0 wne wral wn wbr wi wal wceq anbi2d
        eleq1 imaeq2 sseq1d anbi12d eleq2d notbid imbi1d imbi2d albidv cfv wrex
        raleq imbi12d wfun tfrlem7 fvelima mpan crab cuni cres tfr2 syl adantll
        onelon adantlr copab fveq1i cvv vex resfunexg mp2an rabex eqeltri uniex
        cab crn rneq df-ima syl6eqr raleqdv rabbidv abbidv df-rab unieqd fvopab
        3eqtr4g eqtri syl6eq eqeq1d wsbc wreu simpll ad2antrr ssrab2 a1i neeq1d
        eqsstri rcla4cv adantl ad2antlr imp wereu syl3anc reuunisbc breq2 ax-mp
        wb ralbidv ax-17 breq1 syl5bi mpd sylan simpr syl5ibcom ex cbvsbcv hbeq
        hbra1 hbrab hbuni ralbid sbcie df-ral simprl a4v simplll adantr wereucl
        bitri elrab2 ra4 com12 adantld simpllr onelss imass2 sselda idd embantd
        mpid impancom sylan2 ralrimiv jca eleq2i elrab bitr2i sylib hbbr cla4gf
        hbn hbim syl5com simplrr syl6req cdm tfr1 fndm syl6eleqr jctil funfvima
        wo wfn sylc eqeltrd mtod ioran sylanbrc wor weso sseldi sotric syl12anc
        simplrl mpbird syld com23 sylbid rexlimdva syl5 impr chvarv ) GKUIZUHUJ
        ZUKULZUMZPUXIUNZGUOZRUPUQZBUXIURZUMZUMZCUJZGULZUXRUXLULZUSZUMZNUJZUXLUL
        ZUMZUMZOUJZUYCKUTZUYGUXLULZUYGUXRKUTZVAZVAZOVBZUYCUXRKUTZVAZVAUXHAUJZUK
        ULZUMZPUYPUNZGUOZUXNBUYPURZUMZUMZUXSUXRUYSULZUSZUMZUYCUYSULZUMZUMZUYHUY
        GUYSULZUYJVAZVAZOVBZUYNVAZVAUHAUXIUYPVCZUYFVUIUYOVUNVUOUXQVUCUYEVUHVUOU
        XKUYRUXPVUBVUOUXJUYQUXHUXIUYPUKVEVDVUOUXMUYTUXOVUAVUOUXLUYSGUXIUYPPVFZV
        GUXNBUXIUYPVPVHVHVUOUYBVUFUYDVUGVUOUYAVUEUXSVUOUXTVUDVUOUXLUYSUXRVUPVIV
        JVDVUOUXLUYSUYCVUPVIVHVHVUOUYMVUMUYNVUOUYLVULOVUOUYKVUKUYHVUOUYIVUJUYJV
        UOUXLUYSUYGVUPVIVKVLVMVKVQUXQUYBUYDUYOUYDUYPPVNZUYCVCZAUXIVOZUXQUYBUMZU
        YOPVRZUYDVUSSTHLPQUBUCVSZAUYCUXIPVTWAVUTVURUYOAUXIVUTUYPUXIULZUMZVURFUJ
        ZEUJZKUTZUSZFJURZEJWBZWCZUYCVCZUYOVVDVUQVVKUYCVVDVUQPUYPWDZQVNZVVKUXQVV
        CVUQVVNVCZUYBUXKVVCVVOUXPUXJVVCVVOUXHUXJVVCUMZUYQVVOUXIUYPWHZSTAHLPQUBU
        CWEWFWGWIWIZVVNVVMTUJVVHFIURZEIWBZWCZVCLTWJZVNVVKVVMQVWBUFWKLTVVMVWAVVK
        VVAUYPWLULVVMWLULVVBAWMPUYPWLWNWOVVJVVIEJJMUJZDUJZKUTZMUYSURZDGWBZWLUEV
        WFDGUAWPWQZWPWRZLUJZVVMVCZVVTVVJVWKVVFIULZVVSUMZEWSVVFJULZVVIUMZEWSVVTV
        VJVWKVWMVWOEVWKVWLVWNVVSVVIVWKIJVVFVWKVWEMVWJWTZURZDGWBVWGIJVWKVWQVWFDG
        VWKVWEMVWPUYSVWKVWPVVMWTUYSVWJVVMXAPUYPXBXCXDXEUDUEXJZVIVWKVVHFIJVWRXDV
        HXFVVSEIXGVVIEJXGXJXHXIXKZXLXMVVDUYGVVKKUTZUYKVAZOVBZVVKUXRKUTZVAZVVLUY
        OVVDVVIEVVKXNZVXDVVDVVIEJXOZVXEVVDUXHJGUOZJUPUQZVXFUXQUXHUYBVVCUXHUXJUX
        PXPXQZVXGVVDJVWGGUEVWFDGXRYAZXSVUTVVCVXHUXPVVCVXHVAZUXKUYBUXOVXKUXMUXNV
        XHBUYPUXIBUJZUYPVCZRJUPVXMVWEMPVXLUNZURZDGWBVWGRJVXMVXOVWFDGVXMVWEMVXNU
        YSVXLUYPPVFXDXEUGUEXJXTYBZYCYDYEZEFGJKVWHYFYGVVIEJYHWFVXEVVEVVKKUTZUSZF
        JURZVVDVXDVXEVVEVWDKUTZUSZFJURZDVVKXNZVXTVVKWLULVXEVYDYKVWIVVIVYCEDVVKW
        LVVFVWDVCZVVHVYBFJVYEVVGVYAVVFVWDVVEKYIVJYLUUAYJVYCVXTDVVKVWIVWDVVKVCZV
        YBVXSFJFNNVWDVVKUYCVWDULFYMFNVVJVVIFENJVVHFJUUCUYCJULFYMUUDUUEZUUBVYFVY
        AVXRVWDVVKVVEKYIVJUUFUUGUUNVVDVXBVXTVXCVVDVXBVXTVXCVAVXTVVEJULZVXSVAZFV
        BZVVDVXBUMZVXCVXSFJUUHVYKVYJUXRVVKKUTZUSZVXCVYKUXRJULZVYJVYMVYKUXSVWCUX
        RKUTZMUYSURZUMZVYNVYKUXSVYPVUTUXSVVCVXBUXQUXSUYAUUIXQVYKVYOMUYSVXBVVDVW
        CVVKKUTZVWCUXLULZVYOVAZVAZVWCUYSULZVYOVAVXAWUAOMUYGVWCVCZVWTVYRUYKVYTUY
        GVWCVVKKYNWUCUYIVYSUYJVYOUYGVWCUXLVEUYGVWCUXRKYNVQVQUUJVVDWUBWUAVYOVVDW
        UBUMZVYRVYTVYOWUDVVKJULZVYRWUDUXHVXGVXHWUEVUTUXHVVCWUBUXHUXJUXPUYBUUKXQ
        VXGWUDVXJXSVVDVXHWUBUXQVVCVXHUYBUXPVVCVXHUXKUXOVVCVXHUXMUXOVVCVXHVXPYEW
        GWGWIUULEFGJKVWHUUMZYGWUEVVKGULZVYRMUYSURZUMWUDVYRVWFWUHDVVKGJVYFVWEVYR
        MUYSVWDVVKVWCKYIYLUEUUOWUDWUHVYRWUGWUBWUHVYRVAVVDWUHWUBVYRVYRMUYSUUPUUQ
        YCUURYOYPWUDVYTVYSVYOVVDUYPUXIUOZWUBVYSVUTUXJVVCWUIUXHUXJUXPUYBUUSUXJVV
        CWUIUXIUYPUUTYEYQWUIUYSUXLVWCUYPUXIPUVAUVBYQWUDVYTUVCUVEUVDUVFUVGUVHUVI
        VYNUXRVWGULVYQJVWGUXRUEUVJVWFVYPDUXRGVWDUXRVCVWEVYOMUYSVWDUXRVWCKYIYLUV
        KUVLUVMUXRWLULVYJVYNVYMVAZVACWMVYIWUJFNUXRWLUYCUXRULFYMZVYNVYMFVYNFYMVY
        LFFNUXRVVKKWUKUYCKULFYMVYGUVNUVPUVQVVEUXRVCZVYHVYNVXSVYMVVEUXRJVEWULVXR
        VYLVVEUXRVVKKYNVJVQUVOYJUVRVYKVYMVXCVYKVYMUMZVXCVVKUXRVCZVYLUWGUSZWUMWU
        NUSVYMWUOWUMWUNUXTVVDUYAVXBVYMUXQUXSUYAVVCUVSXQVVDWUNUXTVAVXBVYMVVDVVKU
        XLULWUNUXTVVDVVKVUQUXLVVDVUQVVNVVKVVRVWSUVTVVDVVAUYPPUWAZULZUMVVCVUQUXL
        ULVVDWUQVVAUXQVVCWUQUYBUXKVVCWUQUXPUXJVVCWUQUXHVVPUYPUKWUPVVQPUKUWHWUPU
        KVCSTHLPQUBUCUWBUKPUWCYJUWDWGWIWIVVBUWEVUTVVCYRUXIUYPPUWFUWIUWJVVKUXRUX
        LVEYSXQUWKVYKVYMYRWUNVYLUWLUWMWUMGKUWNZWUGUXSVXCWUOYKVVDWURVXBVYMUXQWUR
        UYBVVCUXHWURUXJUXPGKUWOXQXQXQWUMJGVVKVXJWUMUXHVXGVXHWUEVVDUXHVXBVYMVXIX
        QVXGWUMVXJXSVVDVXHVXBVYMVXQXQWUFYGUWPVVDUXSVXBVYMUXQUXSUYAVVCUWSXQGVVKU
        XRKUWQUWRUWTYTUXAYOYTUXBYOYPVVLVXBUYMVXCUYNVVLVXAUYLOVVLVWTUYHUYKVVKUYC
        UYGKYIVKVMVVKUYCUXRKYNVQYSUXCUXDUXEUXFUXG $.
        $( [20-Oct-2009] $)

      $( Lemma for ~ ordtype . ` F ` maps some ordinal isomorphically onto
         ` A ` . $)
      ordtypelem7 $p |- ( R We A
            -> E. x e. On ( F |` x ) Isom _E , R ( x , A ) ) $=
        ( vz vs vn vm wwe c0 wceq wne cv wral wa con0 wrex cep cres ordtypelem4
        wiso wcel wf1o wbr cfv wfn ccnv wfun crn wss tfr1 onss fnssres ad2antlr
        wb sylancr ordtypelem5 cima df-ima tfrlem7 fvelima mpan ax-17 hbra1 ra4
        hban wi onelon crab ssrab2 ordtypelem1 3expb sseldi eleq1 syl5ib sylani
        eqsstri com12 exp43 imp syl5 adantrl simpll a1i wal breq1 notbid notbii
        wn elrab bitr4i albii syl5bi expr imp3a ad2antrr hbne ex adantl cvv vex
        ne0i ax-mp rabex syl3anc syl syl6 adantll imaeq2 raleqdv rabbidv syl5eq
        csb csbief ordtypelem2 wo word eloni con2bid fvres com3r a2d ssrdv cuni
        rexlimd wex nss simpl iman imbi2i ordtypelem6 con3d con2b df-ral ralbii
        ralrimiv ralnex bitri sylib rexnal simprl syl5ss hbrab1 sylbir funimaex
        wreu rabid rexlimi wereu reurex syl5bir mt3d jcad breq2 ralbidv syl6ibr
        exlimdv impr wereucl syl6eleqr con4d com23 impancom jcai sylibr syl5eqr
        n0i eqss dff1o2 syl3anbrc ad2ant2r simplll hbcsb1 csbeq1a neeq1d impcom
        rcla4 ad2ant2lr 3eqtr4g cbvcsbv neeq1i syl3an3b fveq2 ad2ant2rl orim12d
        ad2ant2l ordtri2 syl2anc wor weso ad3antrrr hbss sseq1d sotric syl12anc
        chvar 3imtr3d impcon4bid epel 3bitr4d ralrimdva ralrimi df-iso sylanbrc
        breqan12rd exp31 reximdvai mpd ) FJUIZIUJUKZOUJULZBAUMZUNZUOZAUPUQUYLFU
        RJMUYLUSZVAZAUPUQABCDEFGHIJKLMNOPQRSTUAUBUCUDUTUYIUYNUYPAUPUYIUYLUPVBZU
        YNUYPUYIUYQUOZUYNUOZUYLFUYOVCZBUMZUEUMZURVDZVUAUYOVEZVUBUYOVEZJVDZVOZUE
        UYLUNZBUYLUNZUYPUYSUYOUYLVFZUYOVGVHUYOVIZFUKUYTUYQVUJUYIUYNUYQMUPVFUYLU
        PVJVUJPQGKMNSTVKUYLVLUPUYLMVMVPVNABCDEFGHIJKLMNOPQRSTUAUBUCUDVQUYSVUKMU
        YLVRZFMUYLVSUYSVULFVJZFVULVJZUOVULFUKUYSVUMVUNUYRUYMVUMUYJUYRUYMUOZUFVU
        LFUFUMZVULVBZVUAMVEZVUPUKZBUYLUQZVUOVUPFVBZMVHZVUQVUTPQGKMNSTVTZBVUPUYL
        MWAWBVUOVUSVVABUYLUYRUYMBUYRBWCUYKBUYLWDWFZVVABWCUYRUYMVUAUYLVBZVUSVVAW
        GZWGZUYMVVEUYKWGUYRVVGUYKBUYLWEZUYRVVEUYKVVFUYIUYQVVEUYKVVFWGZWGUYQVVEU
        YIVVIUYQVVEUYIUYKVVFVUSUYQVVEUOZUYIUYKUOZUOVVAVVJVUSVUAUPVBZVVKVVAUYLVU
        AWHZVVLVVKUOZVURFVBZVUSVVAVVNOFVUROLUMZCUMZJVDZLMVUAVRZUNZCFWIZFUDVVTCF
        WJWQZVVLUYIUYKVUROVBZBACDEFGHOJKLMNIPQRSTUAUDUCUBWKZWLWMVURVUPFWNWOWPWR
        WSUUAWTUUBXAWTUUEXAUUCXBUYRVUMUYNVUNUYRVUMUOZUYJUYMVUNVWEUYMUYJVUNUYRVU
        MUYMUYJVUNWGUYRVUMUYMUOZUOZVUNUYJVUNXIVUBFVBZVUBVULVBXIZUOZUEUUFZVWGUYJ
        XIZUEFVULUUGVWGVWKUGUMZUHUMZJVDZXIZUGVVRLVULUNZCFWIZUNUHVWRWIUUDZIVBZVW
        LUYRVWFVWKVWTUYRVWFVWKUOZUOZVWSVWRIVXBUYIVWRFVJZVWRUJULZVWSVWRVBUYIUYQV
        XAXCVXCVXBVWQCFWJXDUYRVWFVWKVXDVWGVWJVXDUEVWGVWJVUBVWRVBZVXDVWGVWJVWHVV
        PVUBJVDZLVULUNZUOVXEVWGVWJVWHVXGVWJVWHWGVWGVWHVWIUUHXDVWGVWJVXGVWGVWJUO
        ZVXGVWPUGVXFXIZLVULWIZUNZUHVXJUQZVXHVWOVWMVXJVBZXIZWGZUGXEZXIZUHVXJUNZV
        XLXIZVXHVXQUHVXJVWNVXJVBVWNVULVBZVWNVUBJVDZXIZUOVXHVXQVXIVYBLVWNVULVVPV
        WNUKVXFVYAVVPVWNVUBJXFXGXJVXHVXTVYBVXQVWGVWJVXTVYBVXQWGVWGVWJVXTUOUOZVX
        PVYAVXPVWOVWMVULVBZVWMVUBJVDZWGZWGZUGXEVYCVYAVXOVYGUGVXNVYFVWOVXNVYDVYE
        XIZUOZXIVYFVXMVYIVXIVYHLVWMVULVVPVWMUKVXFVYEVVPVWMVUBJXFXGXJXHVYDVYEUUI
        XKUUJXLABUECDEFGHIJKLUHUGMNOPQRSTUAUBUCUDUUKXMUULXNXOXMUUPVXRVXKXIZUHVX
        JUNVXSVXQVYJUHVXJVXPVXKVXPVXMVWPWGZUGXEVXKVXOVYKUGVWOVXMUUMXLVWPUGVXJUU
        NXKXHUUOVXKUHVXJUUQUURUUSVXGXIVXILVULUQZVXHVXLVXFLVULUUTVXHVYLVXLVXHVYL
        UOZVXKUHVXJUVFZVXLVYMUYIVXJFVJVXJUJULZVYNVWGUYIVWJVYLUYIUYQVWFXCXPVYMVX
        JVULFVXILVULWJVWGVUMVWJVYLUYRVUMUYMUVAXPUVBVYLVYOVXHVXIVYOLVULLUHVXJUJV
        XILUHVULUVCVWNUJVBZLWCXQVVPVULVBZVXIVYOVYQVXIUOVVPVXJVBVYOVXILVULUVGVXJ
        VVPYBUVDXRUVHXSUHUGFVXJJVXILVULVVBVULXTVBVVCMUYLAYAUVEYCYDUVIYEVXKUHVXJ
        UVJYFXRUVKUVLXRUVMVWQVXGCVUBFVVQVUBUKVVRVXFLVULVVQVUBVVPJUVNUVOXJUVPVWR
        VUBYBYGUVQUVRUHUGFVWRJVWQCFRYDUVSYEUBUVTXNIVWSUWGYGXMUWAXNUWBXOUWCUWDVU
        LFUWHUWEUWFUYLFUYOUWIUWJUYRUYMVUIUYJVUOVUHBUYLVVDVUOVVEVUGUEUYLVUOVUBUY
        LVBZVVEVUGVUOVYRVVEUOZUOZVUAVUBVBZVURVUBMVEZJVDZVUCVUFVYTWUAWUCVYTVUBUP
        VBZUYIBVUBOYMZUJULZWUAWUCWGZUYRVYRWUDUYMVVEUYQVYRWUDUYIUYLVUBWHYHUWKZUY
        IUYQUYMVYSUWLZUYMVYRWUFUYRVVEVYRUYMWUFUYKWUFBVUBUYLBUHWUEUJBUHVUBOVWNVU
        BVBBWCUWMZVYPBWCXQVUAVUBUKZOWUEUJBVUBOUWNZUWOUWQUWPUWRZWUFWUDUYIAVUBIYM
        ZUJULWUGWUEWUNUJVUBXTVBWUEWUNUKUEYAZBAVUBOIXTVUAUYLUKZVWAVWROIWUPVVTVWQ
        CFWUPVVRLVVSVULVUAUYLMYIYJYKUDUBUWSUWTYCUXAUEBCDEFGHWUNJKLMNOPQRSTUAAUH
        VUBIVVRLMVUBVRZUNZCFWIZWUOVWNWUSVBZAWCUYLVUBUKZIVWRWUSUBWVAVWQWURCFWVAV
        VRLVULWUQUYLVUBMYIYJYKYLYNZUCUDYOUXBYEVYTWUKVUBVUAVBZYPZVURWUBUKZWUBVUR
        JVDZYPZWUAXIZWUCXIZVYTWUKWVEWVCWVFWUKWVEWGVYTVUAVUBMUXCXDVYTVVLUYIUYKWV
        CWVFWGUYRVVEVVLUYMVYRUYQVVEVVLUYIVVMYHUXDZWUIUYMVVEUYKUYRVYRUYMVVEUYKVV
        HWTUXFZBUECDEFGHOJKLMNWUNPQRSTUAUDUCWVBYOYEUXEVYTVUAYQZVUBYQZWVDWVHVOVY
        TVVLWVLWVJVUAYRYFVYTWUDWVMWUHVUBYRYFWVLWVMUOWUAWVDVUAVUBUXGYSUXHVYTFJUX
        IZVVOWUBFVBZWVGWVIVOUYIWVNUYQUYMVYSFJUXJUXKVYTOFVURVWBVYTVVLUYIUYKVWCWV
        JWUIWVKVWDYEWMVYTWUEFWUBOFVJWUEFVJBUEBUHWUEFWUJVWNFVBBWCUXLWUKOWUEFWULU
        XMVWBUXPVYTWUDUYIWUFWUBWUEVBWUHWUIWUMUEACDEFGHWUEJKLMNIPQRSTUABUHVUBOWU
        SWUOWUTBWCWUKOVWAWUSUDWUKVVTWURCFWUKVVRLVVSWUQVUAVUBMYIYJYKYLYNUCUBWKYE
        WMWVNVVOWVOUOUOWUCWVGFVURWUBJUXNYSUXOUXQUXRVUCWUAVOVYTBUEUXSXDVYSVUFWUC
        VOVUOVVEVYRVUDVURVUEWUBJVUAUYLMYTVUBUYLMYTUYEXSUXTXNUYAUYBXBBUEUYLFURJU
        YOUYCUYDUYFUYGUYH $.
        $( [17-Oct-2009] $)
    $}

    $( For any well-ordered set, there is an isomorphic ordinal number called
       its order type.  (Contributed by Jeff Hankins, 17-Oct-2009.) $)
    ordtype $p |- ( ( A e. B /\ R We A )
           -> E! x e. On E. f f Isom _E , R ( x , A ) ) $=
      ( vw vu vh vb vp vj wcel cv cep wiso con0 wceq wral wbr crab vy va vg wwe
      vr vs vo vm vn vk vz vq vv vc wa wrex wi wreu weeq2 isoeq5 exbidv rexbidv
      wex imbi12d wfn cfv cres wn crn cuni copab cab cima rdglem1 eqid wb breq1
      vex cbvralv a1i rabbiia breq2 ralbidv cbvrabv eqtri notbid unieqi opabbii
      eqeq2i raleqdv rabbidv rabeqbidv unieqd eqeq2d eqeq1 sylan9bb ordtypelem7
      rneq cbvopabv cvv tfrlem7 resfunexg mp2an isoeq1 cla4ev reximi syl vtoclg
      wfun imp hbe1 ax-17 hbim cbvexv ccnv isocnv ccom isotr cnvex coex syl5ibr
      ordiso syl5com syl5 exlimdv syl5bi exlimi com12 rgen2a isoeq4 reu4 sylibr
      ex jctir ) BCLZBDUDZUOZAMZBNDEMZOZEVCZAPUPZUUAUAMZBNDYSOZEVCZUOZYRUUCQZUQ
      ZUAPRAPRZUOUUAAPURYQUUBUUIYOYPUUBUBMZDUDZYRUUJNDYSOZEVCZAPUPZUQYPUUBUQUBB
      CUUJBQZUUKYPUUNUUBUUJBDUSUUOUUMUUAAPUUOUULYTEYRUUJBNDYSUTVAVBVDUUKYRUUJND
      UCMZUEMZVEUFMZUUPVFUUPUURVGUGMZUHMZUIMZDSZVHZUHUJMZUKMZDSZUJULMZVIZRZUKUU
      JTZRZUIUVJTZVJZQZULUGVKZVFQUFUUQRUOUEPUPUCVLZVJZYRVGZOZAPUPUUNAUAFUMGUUJU
      VPUVFUJHMZVIZRZUKUUJTZIMZJMZDSZIUVQYRVMZRZJUUJTZDHKUVQUVOUWFIUVQUUCVMZRZJ
      UUJTZIUNUBVRUEUFIUNUCHUVOVNZUVQVOZUWCKMZUVEDSZKUWARZUKUUJTUWOFMZDSZKUWARZ
      FUUJTUWBUWQUKUUJUWBUWQVPUVEUUJLUVFUWPUJKUWAUVDUWOUVEDVQVSVTWAUWQUWTUKFUUJ
      UVEUWRQUWPUWSKUWAUVEUWRUWODWBWCWDWEUWIUWOUWEDSZKUWGRZJUUJTUWSKUWGRZFUUJTU
      WHUXBJUUJUWHUXBVPUWEUUJLZUWFUXAIKUWGUWDUWOUWEDVQZVSVTWAUXBUXCJFUUJUWEUWRQ
      ZUXAUWSKUWGUWEUWRUWODWBZWCWDWEUVOUUSGMZUMMZDSZVHZGUVJRZUMUVJTZVJZQZULUGVK
      UNMZUXKGUWCRZUMUWCTZVJZQZHUNVKUVNUXOULUGUVMUXNUUSUVLUXMUVLUXHUVADSZVHZGUV
      JRZUIUVJTUXMUVKUYCUIUVJUVKUYCVPUVAUVJLUVCUYBUHGUVJUUTUXHQUVBUYAUUTUXHUVAD
      VQWFVSVTWAUYCUXLUIUMUVJUVAUXIQZUYBUXKGUVJUYDUYAUXJUVAUXIUXHDWBWFWCWDWEWGW
      IWHUXOUXTULUGHUNUVGUVTQZUXOUUSUXSQUUSUXPQUXTUYEUXNUXSUUSUYEUXMUXRUYEUXLUX
      QUMUVJUWCUYEUVIUWBUKUUJUYEUVFUJUVHUWAUVGUVTWRWJWKZUYEUXKGUVJUWCUYFWJWLWMW
      NUUSUXPUXSWOWPWSWEUWLUXAKUWJRZJUUJTUWSKUWJRZFUUJTUWKUYGJUUJUWKUYGVPUXDUWF
      UXAIKUWJUXEVSVTWAUYGUYHJFUUJUXFUXAUWSKUWJUXGWCWDWEWQUVSUUMAPUULUVSEUVRUVQ
      XIYRWTLUVRWTLIUNUVPHUVQUVOUWMUWNXAAVRUVQYRWTXBXCYRUUJNDUVRYSXDXEXFXGXHXJU
      UHAUAPUUFYRPLUUCPLUOZUUGUUAUUEUYIUUGUQZYTUUEUYJUQEUUEUYJEUUDEXKUYJEXLXMUU
      EUUCBNDUUPOZUCVCYTUYJUUDUYKEUCUUCBNDUUPYSXDXNYTUYKUYJUCUYKBUUCDNUUPXOZOZY
      TUYJUUCBNDUUPXPYTUYMUYJYTUYMUOYRUUCNNUYLYSXQZOZUYIUUGYRBUUCNDNUYLYSXRUYOU
      UGUYIYRUUCNNUVTOZHVCUYPUYOHUYNUYLYSUUPUCVRXSEVRXTYRUUCNNUYNUVTXDXEYRUUCHY
      BYAYCYMYDYEYFYGXJYHYIYNUUAUUEAUAPUUGYTUUDEYRBUUCNDYSYJVAYKYL $.
      $( [17-Oct-2009] $)
  $}

  ${
    $d A b f g r y $.  $d A x y $.  $d R b g r $.  $d f s t w y z $.
    hartogslem.1 $e |- A e. _V $.
    hartogslem.2 $e |- F = { <. <. b , r >. , y >. |
       ( ( b e. ~P A /\ ( r C_ ( b X. b ) /\ r We b ) ) /\
         ( y e. On /\ E. g g Isom _E , r ( y , b ) ) ) } $.
    hartogslem.3 $e |- R = { <. s , t >. | E. w e. y E. z e. y
       ( ( s = ( f ` w ) /\ t = ( f ` z ) ) /\ w _E z ) } $.
    $( Lemma for ~ hartogs . $)
    hartogslem $p |- { x e. On | x ~<_ A } e. _V $=
      ( wcel cv wa cep cdm cvv cdom wbr con0 crab wfo cpw cxp pwex xpex wss wwe
      wiso wex copab simpl elpwi xpss12 syl2anc sstr sylan2 elpw2 sylibr ancoms
      adantrr jca adantr exlimiv copab2 dmeqi dmoprab eqtri df-xp 3sstr4i ssexi
      ssopab2i crn wfun wmo wi weu wreu ordtype adantrl df-reu eumo syl moanimv
      sylib mpbir funoprab funeqi funforn mpbi wb cab rnoprab rneqi breq1 elrab
      wceq wf1 brdom cin wf f1f frn adantl word eloni ordwe wf1o f1f1orn f1oiso
      cfv wrex mpan2 isores2 isowe 3syl syl5ib impcom inss2 jctil isoeq1 cla4ev
      vex rnex inex2 eleq1 simpr xpeq1 xpeq2 eqtrd sseq12d anbi12d expcom ax-mp
      sylan9bb weeq2 weeq1 isoeq5 isoeq3 exbidv anbi2d cla42ev sylbi isof1o cen
      syl22anc eximi bren ssdomg endomtr sylan anim2d imp exlimivv impbii bitri
      sylanbr abbi2i 3eqtr4i foeq3 fornex mp2 ) JUAZUBQUVHARZFUCUDZAUEUFZJUGZUV
      KUBQUVHFUHZFFUIZUHZUIZUVMUVOFNUJUVNFFNNUKZUJUKMRZUVMQZLRZUVRUVRUIZULZUVRU
      VTUMZSZSZBRZUEQZUWFUVRTUVTIRZUNZIUOZSZSZBUOZMLUPZUVSUVTUVOQZSZMLUPUVHUVPU
      WMUWPMLUWLUWPBUWEUWPUWKUWEUVSUWOUVSUWDUQUVSUWBUWOUWCUWBUVSUWOUWBUVSSUVTUV
      NULZUWOUVSUWBUWAUVNULZUWQUVSUVRFULZUWSUWRUVRFURZUWTUVRFUVRFUSUTUVTUWAUVNV
      AVBUVTUVNUVQVCVDVEVFVGVHVIVQUVHUWLMLBVJZUAUWNJUXAOVKUWLMLBVLVMMLUVMUVOVNV
      OVPUVHJVRZJUGZUVLJVSZUXCUXDUXAVSUWLMLBUWLBVTUWEUWKBVTZWAUWEUWKBWBZUXEUWEU
      WJBUEWCZUXFUVSUWCUXGUWBBUVRUVMUVTIWDWEUWJBUEWFWJUWKBWGWHUWEUWKBWIWKWLJUXA
      OWMWKJWNWOUXBUVKXBUXCUVLWPUXAVRUWLLUOMUOZBWQUXBUVKUWLMLBWRJUXAOWSUXHBUVKU
      WFUVKQUWGUWFFUCUDZSZUXHUVJUXIAUWFUEUVIUWFFUCWTXAUXJUXHUXIUWGUXHUXIUWFFHRZ
      XCZHUOUWGUXHWAZUWFFHNXDUXLUXMHUWGUXLUXHUWGUXLSZUXKVRZUVMQZGUXOUXOUIZXEZUX
      QULZUXOUXRUMZSZUWGUWFUXOTUXRUWHUNZIUOZUXHUXLUXPUWGUXLUXOFULZUXPUXLUWFFUXK
      XFUYDUWFFUXKXGUWFFUXKXHWHUXOFNVCVDXIUXNUXTUXSUXLUWGUXTUWGUWFTUMZUXLUXTUWG
      UWFXJUYEUWFXKUWFXLWHUXLUWFUXOUXKXMZUWFUXOTUXRUXKUNZUYEUXTWPUWFFUXKXNZUYFU
      WFUXOTGUXKUNZUYGUYFGKRDRZUXKXPXBERCRZUXKXPXBSUYJUYKTUDSCUWFXQDUWFXQKEUPXB
      UYIPDCKEUWFUXOTGUXKXOXRUWFUXOTGUXKXSWJZUWFUXOTUXRUXKXTYAYBYCGUXQYDYEUWGUX
      LUQUXLUYCUWGUXLUYFUYGUYCUYHUYLUYBUYGIUXKHYHZUWFUXOTUXRUXKUWHYFYGYAXIUWLUX
      PUYASZUWGUYCSZSMLUXOUXRUXKUYMYIZUXQGUXOUXOUYPUYPUKYJUVRUXOXBZUVTUXRXBZSZU
      WEUYNUWKUYOUYSUVSUXPUWDUYAUYQUVSUXPWPUYRUVRUXOUVMYKVHUYSUWBUXSUWCUXTUYSUV
      TUXRUWAUXQUYQUYRYLUYQUWAUXQXBUYRUYQUWAUXOUVRUIUXQUVRUXOUVRYMUVRUXOUXOYNYO
      VHYPUYQUWCUXOUVTUMUYRUXTUVRUXOUVTUUAUXOUVTUXRUUBYTYQYQUYSUWJUYCUWGUYSUWIU
      YBIUYQUWIUWFUXOTUVTUWHUNUYRUYBUWFUVRUXOTUVTUWHUUCUWFUXOTUVTUXRUWHUUDYTUUE
      UUFYQUUGUUKYRVIUUHYCUWLUXJMLUWEUWKUXJUWEUWJUXIUWGUVSUWJUXIWAUWDUWJUVSUXIU
      WJUWFUVRUWHXMZIUOZUVSUXIUWIUYTIUWFUVRTUVTUWHUUIUULVUAUWFUVRUUJUDZUVSUXIUW
      FUVRIMYHZUUMUVSVUBUWSUXIUWTUWSVUBUVRFUCUDZUXIUVRUBQUWSVUDWAVUCUVRFUBUUNYS
      UWFUVRFUUOVBVBUVBUUPYRVHUUQUURUUSUUTUVAUVCUVDUXBUVKUVHJUVEYSWOUVHUVKUBJUV
      FUVG $.
      $( [14-Jan-2013] $)
  $}

  ${
    $d A a x y $.  $d A x y z $.  $d a b f g r y $.  $d b f g r s t w y z $.
    $( Given any set, the Hartogs number of the set is the least ordinal not
       dominated by that set.  This theorem proves that there is always an
       ordinal which satisfies this.  (This theorem can be proven trivially
       using the AC - see theorem ~ ondomon - but this proof works in ZF.)
       (Contributed by Jeff Hankins, 22-Oct-2009.)  (Revised by Mario Carneiro,
       14-Jan-2013.) $)
    hartogs $p |- ( A e. V -> { x e. On | x ~<_ A } e. On ) $=
      ( vy vz va vw vt vs vf vb vr wcel cv cdom wbr con0 wss wa cvv vg crab wtr
      word wel wi wal onelon vex onelss imp ssdomg mpsyl jca domtr anim2i sylan
      anassrs exp31 com12 imp3a breq1 elrab gen2 dftr2 mpbir ssrab2 ordon mp3an
      3imtr4g trssord wceq breq2 rabbidv eleq1d cfv cep wrex copab cpw cxp wiso
      wb wwe wex copab2 eqid hartogslem vtoclg elong syl mpbiri ) BCMZANZBOPZAQ
      UBZQMZWPUDZWPUCZWPQRQUDWRWSDEUEZENZWPMZSDNZWPMZUFZEUGDUGXEDEWTXBXDWTXAQMZ
      XABOPZSXCQMZXCBOPZSZXBXDWTXFXGXJXFWTXGXJUFXFWTXGXJXFWTSZXHXCXAOPZSXGXJXKX
      HXLXAXCUHXCTMXKXCXARZXLDUIXFWTXMXAXCUJUKXCXATULUMUNXHXLXGXJXLXGSXIXHXCXAB
      UOUPURUQUSUTVAWOXGAXAQWNXABOVBVCWOXIAXCQWNXCBOVBVCVJUKVDDEWPVEVFWOAQVGVHW
      PQVKVIWMWPTMZWQWRWCWNFNZOPZAQUBZTMXNFBCXOBVLZXQWPTXRXPWOAQXOBWNOVMVNVOADE
      GHXOINGNZUANZVPVLHNXAXTVPVLSXSXAVQPSEXCVRGXCVRIHVSZUAJKNZXOVTMLNZYBYBWARY
      BYCWDSSXHXCYBVQYCJNWBJWESSKLDWFZILKFUIYDWGYAWGWHWIWPTWJWKWL $.
      $( [14-Jan-2013] $)  $( [22-Oct-2009] $)
  $}

  ${
    $d A f x y z $.  $d R f x $.
    wofib.1 $e |- A e. _V $.
    $( The only sets which are well-ordered forwards and backwards are finite
       sets.  (Contributed by Mario Carneiro, 30-Jan-2014.) $)
    wofib $p |- ( ( R Or A /\ A e. Fin ) <-> ( R We A /\ `' R We A ) ) $=
      ( vx vf vz vy wcel wa cv cep con0 wrex cvv syl wi com wbr wn vex wor ccnv
      cfn wwe wofi sylanb jca weso adantr wiso wex wreu ordtype reurex mpan wfr
      cnvso wefr isocnv2 isofr sylbi biimparc wral csuc peano2 sucidg wceq epel
      brcnv bitri eleq2 syl5bb rcla4ev syl2anc dfrex2 sylib nrex wss word ordom
      wb eloni ordtri1 sylancr adantl w3a wne simp3 simp2 ssexg simp1 1onn ne0i
      c0 c1o mp1i fri syl22anc 3expia sylbird mt3i ex wf1o cen f1oen ensym ra4e
      isof1o isfi sylibr expcom 3syl impancom exlimdv rexlimdva mpan9 impbii
      syld ) ABUAZAUCHZIZABUDZABUBZUDZIZYAYBYDABUEXSAYCUAXTYDABUQAYCUEUFUGYEXSX
      TYBXSYDABUHUIYBDJZAKBEJZUJZEUKZDLMZYDXTANHZYBYJCYKYBIYIDLULYJDANBEUMYIDLU
      NOUOYDAYCUPZYJXTPAYCURYLYIXTDLYLYFLHZIYHXTEYLYHYMXTYLYHIZYMYFQHZXTYNYFKUB
      ZUPZYMYOPYHYQYLYHYFAYPYCYGUJYQYLWAYFAKBYGUSYFAYPYCYGUTVAVBYQYMYOYQYMIZYOF
      JZGJZYPRZSFQVCZGQMZUUBGQYTQHZUUAFQMZUUBSUUDYTVDZQHYTUUFHZUUEYTVEYTQVFUUAU
      UGFUUFQUUAYTYSHZYSUUFVGUUGUUAYTYSKRUUHYSYTKFTGTVIGFVHVJYSUUFYTVKVLVMVNUUA
      FQVOVPVQYRYOSZQYFVRZUUCYMUUJUUIWAZYQYMQVSYFVSUUKVTYFWBQYFWCWDWEYQYMUUJUUC
      YQYMUUJWFZQNHZYQUUJQWNWGZUUCUULUUJYMUUMYQYMUUJWHZYQYMUUJWIQYFLWJVNYQYMUUJ
      WKUUOWOQHUUNUULWLQWOWMWPGFYFQNYPWQWRWSWTXAXBOYHYOXTPZYLYHYFAYGXCYFAXDRZUU
      PYFAKBYGXHYFAYGDTXEUUQAYFXDRZUUPYFACXFYOUURXTYOUURIUURDQMXTUURDQXGDAXIXJX
      KOXLWEXRXMXNXOOXPUGXQ $.
      $( [30-Jan-2014] $)
  $}

  ${
    $d x y z A $.
    $( Any ordinal number is strictly dominated by some other ordinal number.
       (Contributed by Jeff Hankins, 22-Oct-2009.) $)
    onsdom $p |- ( A e. On -> E. x e. On A ~< x ) $=
      ( vy vz con0 wcel cv cdom wbr crab csdm cen wn wss wa cvv wi ssdomg breq1
      syl6 wrex hartogs onelon ex onelss vex ax-mp jcad elrab syl6ibr ssrdv mpd
      word eloni ordirr 3syl a1d ancrd cbvrabv eleq2i bitr3i syl6ib mtod ensymg
      syl endom brsdom sylanbrc breq2 rcla4ev syl2anc ) BEFZCGZBHIZCEJZEFZBVOKI
      ZBAGZKIZAEUACBEUBZVLBVOHIZBVOLIZMVQVLBVONWAVLDBVOVLDGZBFZWCEFZWCBHIZOWCVO
      FVLWDWEWFVLWDWEBWCUCUDVLWDWCBNZWFBWCUEWCPFWGWFQDUFWCBPRUGTUHVNWFCWCEVMWCB
      HSUIUJUKBVOERULVLWBVOBHIZVLWHVOVOFZVLVPVOUMWIMVTVOUNVOUOUPVLWHVPWHOZWIVLW
      HVPVLVPWHVTUQURWJVOWFDEJZFWIWFWHDVOEWCVOBHSUIWKVOVOWFVNDCEWCVMBHSUSUTVAVB
      VCVLWBVOBLIZWHVLVPWBWLQVTBVOEVDVEVOBVFTVCBVOVGVHVSVQAVOEVRVOBKVIVJVK $.
      $( [22-Oct-2009] $)
  $}

  ${
    $d A x y z $.
    $( Proof that the alternate definition ~ cardval2 is always a set, and
       indeed is an ordinal.  (Contributed by Mario Carneiro, 14-Jan-2013.) $)
    card2on $p |- { x e. On | x ~< A } e. On $=
      ( vy vz cvv wcel cv csdm wbr con0 crab word wss wa wi wal cdom imp breq1
      c0 wtr wel onelon vex onelss ssdomg mpsyl jca domsdomtr sylan exp31 com12
      anim2i anassrs imp3a elrab 3imtr4g dftr2 mpbir ssrab2 ordon trssord mp3an
      gen2 hartogs sdomdom a1i ss2rabi ssexg mpan elong 3syl mpbiri 0elon eleq1
      wb wceq wrex wne df-ne rabn0 bitr3i sdomex simprd rexlimivw sylbi pm2.61i
      wn nsyl4 ) BEFZAGZBHIZAJKZJFZWJWNWMLZWMUAZWMJMJLWOWPCDUBZDGZWMFZNCGZWMFZO
      ZDPCPXBCDWQWSXAWQWRJFZWRBHIZNWTJFZWTBHIZNZWSXAWQXCXDXGXCWQXDXGOXCWQXDXGXC
      WQNZXEWTWRQIZNXDXGXHXEXIWRWTUCWTEFXHWTWRMZXICUDXCWQXJWRWTUERWTWREUFUGUHXE
      XIXDXGXIXDNXFXEWTWRBUIUMUNUJUKULUOWLXDAWRJWKWRBHSUPWLXFAWTJWKWTBHSUPUQRVD
      CDWMURUSWLAJUTVAWMJVBVCWJWKBQIZAJKZJFZWMEFZWNWOVPABEVEWMXLMXMXNWLXKAJWLXK
      OWKJFWKBVFVGVHWMXLJVIVJWMEVKVLVMWMTVQZWNWJXOWNTJFVNWMTJVOVMXOWHZWLAJVRZWJ
      XPWMTVSXQWMTVTWLAJWAWBWLWJAJWLWKEFWJWKBWCWDWEWFWIWG $.
      $( [14-Jan-2013] $)
  $}

  ${
    $d A x y n $.
    $( The definition ~ cardval2 has the curious property that for
       non-numerable sets (for which ~ cardval3b yields ` (/) ` ), it still
       evaluates to a non-empty set, and indeed it contains ` om ` .
       (Contributed by Mario Carneiro, 15-Jan-2013.) $)
    card2inf $p |- ( -. E. y e. On y ~~ A -> om C_ { x e. On | x ~< A } ) $=
      ( vn cv cen wbr con0 wn csdm com wss wcel breq1 0elon rcla4ev cdom brsdom
      c0 mpbiran wrex wral crab csuc mpan con3i 0dom sylibr wa wi sucdom2 enref
      cvv 0ex brprc mpbiri sylancr nsyl4 imp adantl nnon suceloni ex 3syl con3d
      adantrr sylanbrc exp32 finds2 com12 ralrimiv omsson ssrab ) BEZCFGZBHUAZI
      ZAEZCJGZAKUBZKVSAHUCLZVQVSAKVRKMVQVSVSSCJGZDEZCJGZWCUDZCJGZVQADVRSCJNVRWC
      CJNVRWECJNVQSCFGZIZWBWGVPSHMZWGVPOVOWGBSHVNSCFNPZUEUFWBSCQGWHCUGSCRTUHWCK
      MZVQWDWFWKVQWDUIZUIWECQGZWECFGZIZWFWLWMWKVQWDWMCUMMZWDWMUJVPWCCUMUKWPIZWI
      WGVPOWQWGSSFGSUNULSCFUOUPWJUQURUSUTWKVQWOWDWKVQWOWKWNVPWKWCHMWEHMZWNVPUJW
      CVAWCVBWRWNVPVOWNBWEHVNWECFNPVCVDVEUSVFWECRVGVHVIVJVKWAKHLVTVLVSAHKVMTUH
      $.
      $( [15-Jan-2013] $)
  $}

