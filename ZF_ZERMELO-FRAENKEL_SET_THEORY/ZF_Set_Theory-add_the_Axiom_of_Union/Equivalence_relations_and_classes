$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Equivalence relations and classes
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Introduce new constant symbols. $)
  $c Er $.  $( Equivalence predicate $)
  $c /. $.  $( Long slash for quotient set $)

  $( Extend the definition of a wff to include the equivalence predicate. $)
  wer $a wff Er R $.

  $( Extend the definition of a class to include equivalence class. $)
  cec $a class [ A ] R $.

  $( Extend the definition of a class to include quotient set. $)
  cqs $a class ( A /. R ) $.

  $( Define the equivalence predicate. ` R ` need not be a relation but
     ordinarily will be, in which case we call it an equivalence relation.  Our
     notation is not standard.  A formal notation doesn't seem to exist in the
     literature; instead only informal English tends to be used.  Some
     definitions in the literature may also require that ` R ` be a relation.
     The present definition, although somewhat cryptic, nicely avoids dummy
     variables.  In ~ dfer2 we derive a more typical definition.  We show that
     an equivalence relation is reflexive, symmetric, and transitive in
     ~ erref , ~ ersymb , and ~ ertr . $)
  df-er $a |- ( Er R <-> ( `' R u. ( R o. R ) ) C_ R ) $.

  ${
    $d x y z R $.
    $( Alternate definition of equivalence predicate. $)
    dfer2 $p |- ( Er R <-> A. x A. y A. z
              ( ( x R y -> y R x ) /\ ( ( x R y /\ y R z ) -> x R z ) ) ) $=
      ( wer ccnv ccom cun wss cv wbr wi wal df-er cnvsym cotr albii 19.26 bitri
      wa anbi12i unss 19.28v bitr2i 3bitr3i ) DEDFZDDGZHDIZAJZBJZDKZUJUIDKLZUKU
      JCJZDKTUIUMDKLZTCMZBMZAMZDNUFDIZUGDIZTULBMZAMZUNCMZBMZAMZTZUHUQURVAUSVDAB
      DOABCDPUAUFUGDUBUQUTVCTZAMVEUPVFAUPULVBTZBMVFUOVGBULUNCUCQULVBBRSQUTVCARU
      DUES $.
      $( [3-Jan-1997] $)
  $}

  $( Define the ` R ` -coset of ` A ` .  Exercise 35 of [Enderton] p. 61.  This
     is called the equivalence class of ` A ` modulo ` R ` when ` R ` is an
     equivalence relation (i.e. when ` Er R ` ; see ~ dfer2 ).  In this case,
     ` A ` is a representative (member) of the equivalence class ` [ A ] R ` ,
     which contains all sets that are equivalent to ` A ` .  Definition of
     [Enderton] p. 57 uses the notation ` [ A ] ` (subscript) ` R ` , although
     we simply follow the brackets by ` R ` since we don't have subscripted
     expressions.  For an alternate definition, see ~ dfec2 . $)
  df-ec $a |- [ A ] R = ( R " { A } ) $.

  ${
    $d y A x $.  $d y R x $.
    $( Alternate definition of ` R ` -coset of ` A ` .  Definition 34 of
       [Suppes] p. 81.  (The proof was shortened by Mario Carneiro,
       9-Jul-2014.) $)
    dfec2 $p |- ( A e. V -> [ A ] R = { y | A R y } ) $=
      ( wcel cec csn cima cv wbr cab df-ec imasng syl5eq ) BDEBCFCBGHBAICJAKBCL
      ABDCMN $.
      $( [9-Jul-2014] $) $( [3-Jan-1997] $)
  $}

  $( An equivalence class modulo a set is a set. $)
  ecexg $p |- ( R e. B -> [ A ] R e. _V ) $=
    ( wcel cec csn cima cvv df-ec imaexg syl5eqel ) CBDACECAFZGHACICLBJK $.
    $( [24-Jul-1995] $)

  $( A nonempty equivalence class implies the representative is a set. $)
  ecexr $p |- ( A e. [ B ] R -> B e. _V ) $=
    ( cvv wcel csn cima cec c0 wceq n0i wn snprc imaeq2 sylbi ima0 syl6eq nsyl2
    df-ec eleq2s ) BDEZACBFZGZBCHAUCEUCIJUAUCAKUALZUCCIGZIUDUBIJUCUEJBMUBICNOCP
    QRBCST $.
    $( [9-Jul-2014] $)

  ${
    $d x y A $.  $d x y R $.
    $( Define quotient set. ` R ` is usually an equivalence relation.
       Definition of [Enderton] p. 58. $)
    df-qs $a |- ( A /. R ) = { y | E. x e. A y = [ x ] R } $.
  $}

  $( Equality theorem for equivalence predicate. $)
  ereq $p |- ( R = S -> ( Er R <-> Er S ) ) $=
    ( wceq ccnv ccom cun wss cnveq coeq1 coeq2 eqtrd uneq12d sseq1d sseq2 bitrd
    wer df-er 3bitr4g ) ABCZADZAAEZFZAGZBDZBBEZFZBGZAPBPSUCUFAGUGSUBUFASTUDUAUE
    ABHSUABAEUEABAIABBJKLMABUFNOAQBQR $.
    $( [4-Jun-1995] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    ersym.1 $e |- ( ph -> Er R ) $.
    ersym.2 $e |- ( ph -> A e. V ) $.
    ersym.3 $e |- ( ph -> B e. W ) $.
    ersym.4 $e |- ( ph -> A R B ) $.
    $( An equivalence relation is symmetric. $)
    ersym $p |- ( ph -> B R A ) $=
      ( ccnv wbr wcel wb brcnvg syl2anc mpbird ccom cun ssun1 df-er sylib ssbrd
      wer wss syl5ss mpd ) ACBDKZLZCBDLAUIBCDLZJACFMBEMUIUJNIHCBFEDOPQAUHDCBAUH
      UHDDRZSZDUHUKTADUDULDUEGDUAUBUFUCUG $.
      $( [9-Jul-2014] $) $( [4-Jun-1995] $)
  $}

  ${
    ersymb.1 $e |- ( ph -> Er R ) $.
    ersymb.2 $e |- ( ph -> A e. V ) $.
    ersymb.3 $e |- ( ph -> B e. W ) $.
    $( An equivalence relation is symmetric. $)
    ersymb $p |- ( ph -> ( A R B <-> B R A ) ) $=
      ( wbr wa wer adantr wcel simpr ersym impbida ) ABCDJZCBDJZARKBCDEFADLZRGM
      ABENZRHMACFNZRIMAROPASKCBDFEATSGMAUBSIMAUASHMASOPQ $.
      $( [9-Jul-2014] $) $( [30-Jul-1995] $)
  $}

  ${
    $d A x $.  $d B x y $.  $d R x y z $.
    errsym.1 $e |- ( ph -> Er R ) $.
    errsym.2 $e |- ( ph -> Rel R ) $.
    errsym.3 $e |- ( ph -> B e. V ) $.
    $( An equivalence relation is symmetric.  This version of ~ ersym avoids
       some set existence assumptions when we can be assured that ` R ` is a
       relation.  (Contributed by Mario Carneiro, 6-May-2013.) $)
    errsym $p |- ( ph -> ( A R B -> B R A ) ) $=
      ( wbr wa cvv wer adantr wrel wcel brrelex sylan simpr ersym ex ) ABCDIZCB
      DIAUAJBCDKEADLUAFMADNUABKOGBCDPQACEOUAHMAUARST $.
      $( [9-Jul-2014] $) $( [6-May-2013] $)
  $}

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    ertr.1 $e |- ( ph -> Er R ) $.
    ertr.2 $e |- ( ph -> A e. V ) $.
    ertr.3 $e |- ( ph -> B e. W ) $.
    ertr.4 $e |- ( ph -> C e. X ) $.
    $( An equivalence relation is transitive. $)
    ertr $p |- ( ph -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( vx vy vz cv wbr wi wal wcel wa ccom wss ccnv cun ssun2 wer df-er syl5ss
      sylib cotr wceq wb breq12 3adant3 3adant1 anbi12d 3adant2 imbi12d cla43gv
      w3a syl3anc mpd ) AMPZNPZEQZVEOPZEQZUAZVDVGEQZRZOSNSMSZBCEQZCDEQZUAZBDEQZ
      RZAEEUBZEUCVLAVREUDZVRUEZEVRVSUFAEUGVTEUCIEUHUJUIMNOEUKUJABFTCGTDHTVLVQRJ
      KLVKVQMNOBCDFGHVDBULZVECULZVGDULZVAZVIVOVJVPWDVFVMVHVNWAWBVFVMUMWCVDBVECE
      UNUOWBWCVHVNUMWAVECVGDEUNUPUQWAWCVJVPUMWBVDBVGDEUNURUSUTVBVC $.
      $( [9-Jul-2014] $) $( [4-Jun-1995] $)

    ${
      $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
      ertrd.5 $e |- ( ph -> A R B ) $.
      ertrd.6 $e |- ( ph -> B R C ) $.
      $( A transitivity relation for equivalences. $)
      ertrd $p |- ( ph -> A R C ) $=
        ( wbr ertr mp2and ) ABCEOCDEOBDEOMNABCDEFGHIJKLPQ $.
        $( [9-Jul-2014] $)

      $( A transitivity relation for equivalences. $)
      ertr2d $p |- ( ph -> C R A ) $=
        ( ertrd ersym ) ABDEFHIJLABCDEFGHIJKLMNOP $.
        $( [9-Jul-2014] $)
    $}

    ${
      $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
      ertr3d.5 $e |- ( ph -> B R A ) $.
      ertr3d.6 $e |- ( ph -> B R C ) $.
      $( A transitivity relation for equivalences. $)
      ertr3d $p |- ( ph -> A R C ) $=
        ( ersym ertrd ) ABCDEFGHIJKLACBEGFIKJMONP $.
        $( [9-Jul-2014] $)
    $}

    ${
      ertr4d.5 $e |- ( ph -> A R B ) $.
      ertr4d.6 $e |- ( ph -> C R B ) $.
      $( A transitivity relation for equivalences. $)
      ertr4d $p |- ( ph -> A R C ) $=
        ( ersym ertrd ) ABCDEFGHIJKLMADCEHGILKNOP $.
        $( [9-Jul-2014] $)
    $}
  $}

  ${
    $d A x $.  $d ph x $.  $d R x $.
    erref.1 $e |- ( ph -> Er R ) $.
    erref.2 $e |- ( ph -> dom R = X ) $.
    erref.3 $e |- ( ph -> A e. X ) $.
    $( An equivalence relation is reflexive on its field.  Compare Theorem 3M
       of [Enderton] p. 56.  (Revised by Mario Carneiro, 6-May-2013.) $)
    erref $p |- ( ph -> A R A ) $=
      ( vx cv wbr wex cdm wcel eleqtrrd wb eldmg syl mpbid cvv adantr wer simpr
      wa vex a1i ertr4d ex exlimdv mpd ) ABHIZCJZHKZBBCJZABCLZMZULABDUNGFNABDMZ
      UOULOGHBCDPQRAUKUMHAUKUMAUKUCZBUJBCDSDACUAUKETAUPUKGTZUJSMUQHUDUEURAUKUBZ
      USUFUGUHUI $.
      $( [6-May-2013] $)
  $}

  ${
    $d x y z R $.  $d x y A $.  $d y B $.  $d x y z C $.
    errtr.1 $e |- ( ph -> Er R ) $.
    errtr.2 $e |- ( ph -> Rel R ) $.
    $( An equivalence relation is transitive.  This version of ~ ertr avoids
       some set existence assumptions when we can be assured that ` R ` is a
       relation.  (Revised by Mario Carneiro, 6-May-2013.) $)
    errtr $p |- ( ph -> ( ( A R B /\ B R C ) -> A R C ) ) $=
      ( wbr wa cvv wcel simplr cdm wer ad2antrr wrel simpl syl2an adantr simpr
      releldm brrelex ertr mpd wn eqidd erref wb brprc adantl mpbird pm2.61dan
      ex ) ABCEHZCDEHZIZBDEHZAUPIZDJKZUQURUSIZUPUQAUPUSLUTBCDEEMZJJAENZUPUSFOUR
      BVAKZUSAEPZUNVCUPGUNUOQBCEUARZSURCJKZUSAVDUOVFUPGUNUOTCDEUBRSURUSTUCUDURU
      SUEZIUQBBEHZURVHVGURBEVAAVBUPFSURVAUFVEUGSVGUQVHUHURBDEUIUJUKULUM $.
      $( [9-Jul-2014] $) $( [4-Jun-1995] $)
  $}

  ${
    $d x y R $.
    $( The range and domain of an equivalence relation are equal.  (Contributed
       by Rodolfo Medina, 11-Oct-2010.) $)
    erdmrn $p |- ( Er R -> dom R = ran R ) $=
      ( vx vy wer cdm crn cv wbr wex wcel cvv id vex ersymb exbidv eldm 3bitr4g
      a1i elrn eqrdv ) ADZBAEZAFZUABGZCGZAHZCIUEUDAHZCIUDUBJUDUCJUAUFUGCUAUDUEA
      KKUALUDKJUABMZRUEKJUACMRNOCUDAUHPCUDAUHSQT $.
      $( [11-Oct-2010] $)
  $}

  ${
    $d x y z R $.
    iser.1 $e |- ( x R y -> y R x ) $.
    iser.2 $e |- ( ( x R y /\ y R z ) -> x R z ) $.
    $( A symmetric, transitive relation is an equivalence relation. $)
    iser $p |- Er R $=
      ( wer cv wbr wi wa wal dfer2 pm3.2i gen2 mpgbir ) DGAHZBHZDIZRQDIJZSRCHZD
      IKQUADIJZKZCLBLAABCDMUCBCTUBEFNOP $.
      $( [4-Jun-1995] $)
  $}

  ${
    $d x y z R $.  $d x A $.  $d x y z ph $.
    iserd.1 $e |- ( ( ph /\ x R y ) -> y R x ) $.
    iserd.2 $e |- ( ( ph /\ ( x R y /\ y R z ) ) -> x R z ) $.
    $( A symmetric, transitive relation is an equivalence relation. $)
    iserd $p |- ( ph -> Er R ) $=
      ( cv wbr wi wa wal wer ex jca alrimiv dfer2 sylibr ) ABHZCHZEIZTSEIZJZUAT
      DHZEIKZSUDEIZJZKZDLZCLZBLEMAUJBAUICAUHDAUCUGAUAUBFNAUEUFGNOPPPBCDEQR $.
      $( [4-Jun-1995] $)

    iser2d.3 $e |- ( ph -> ( x e. A <-> x R x ) ) $.
    $( A reflexive, symmetric, transitive relation is an equivalence relation
       on its domain. $)
    iser2d $p |- ( ph -> ( Er R /\ dom R = A ) ) $=
      ( wer cdm wceq iserd cv wcel wbr wa adantr eqidd simpr vex breldm impbid1
      erref ex bitr4d eqrdv jca ) AFJZFKZELABCDFGHMZABUJEABNZUJOZULULFPZULEOAUM
      UNAUMUNAUMQZULFUJAUIUMUKRUOUJSAUMTUDUEULULFBUAUBUCIUFUGUH $.
      $( [9-Jul-2014] $)
  $}

  ${
    $d u v w x y z ph $.  $d x y z R $.  $d u v w S $.  $d u x y z X $.
    swoer.1 $e |- S = ( ( X X. X ) \ ( R u. `' R ) ) $.
    swoer.2 $e |- ( ( ph /\ ( y e. X /\ z e. X ) ) ->
                    ( y R z -> -. z R y ) ) $.
    swoer.3 $e |- ( ( ph /\ ( x e. X /\ y e. X /\ z e. X ) ) ->
                    ( x R y -> ( x R z \/ z R y ) ) ) $.
    $( Incomparability under a strict weak partial order is an equivalence
       relation.  (Contributed by Mario Carneiro, 9-Jul-2014.) $)
    swoer $p |- ( ph -> ( Er S /\ dom S = X ) ) $=
      ( cv wbr wa wn brxp wo bitri wi breq1 breq2 vu vv simpr cxp ccnv cun wcel
      vw ancom vex 3bitr4i brcnv bicomi orbi12i orcom brun notbii anbi12i breqi
      cdif brdif sylib simplbi ad2antrl syl ad2antll simprbi ralrimivvva adantr
      sylanbrc orbi1d imbi12d orbi2d orbi12d imbi2d rcla43v syl3anc mpd 3imtr4g
      wral orim12d or4 mtord wpo swopo poirr sylan sylnibr ioran adantl impbida
      wceq iser2d ) AUAUBUHGFAUAKZUBKZFLZMWPWOWNFLZAWPUCWNWOGGUDZLZWNWOEEUEZUFZ
      LZNZMZWOWNWRLZWOWNXALZNZMZWPWQWSXEXCXGWNGUGZWOGUGZMXJXIMWSXEXIXJUIWNWOGGU
      BUJZOZWOWNGGUAUJZOUKXBXFWNWOELZWNWOWTLZPZWOWNELZWOWNWTLZPZXBXFXPXRXQPXSXN
      XRXOXQXRXNWOWNEXKXMULUMWNWOEXMXKULZUNXRXQUOQWNWOEWTUPZWOWNEWTUPUKUQURWPWN
      WOWRXAUTZLXDWNWOFYBHUSWNWOWRXAVAQZWQWOWNYBLXHWOWNFYBHUSWOWNWRXAVAQUKVBAWP
      WOUHKZFLZMZMZWNYDWRLZWNYDXALZNZWNYDFLZYGXIYDGUGZYHYGWSXIWPWSAYEWPWSXCYCVC
      VDZWSXIXJXLVCVEZYGWOYDWRLZYLYEYOAWPYEYOWOYDXALZNZYEWOYDYBLYOYQMWOYDFYBHUS
      WOYDWRXAVAQZVCVFYOXJYLWOYDGGUHUJZOVGVEZWNYDGGYSOVJYGYIXBYPWPXCAYEWPWSXCYC
      VGVDYEYQAWPYEYOYQYRVGVFYGWNYDELZWNYDWTLZPXNWOYDELZPZXOWOYDWTLZPZPZYIXBYPP
      ZYGUUAUUDUUBUUFYGBKZCKZELZUUIDKZELZUULUUJELZPZRZDGVTCGVTBGVTZUUAUUDRZAUUQ
      YFAUUPBCDGGGJVHVIZYGXIYLXJUUQUURRYNYTYGWSXJYMWSXIXJXLVGVEZUUPUURWNUUJELZW
      NUULELZUUNPZRUUAUVBUULYDELZPZRBCDWNYDWOGGGUUIWNWLZUUKUVAUUOUVCUUIWNUUJESU
      VFUUMUVBUUNUUIWNUULESVKVLUUJYDWLZUVAUUAUVCUVEUUJYDWNETUVGUUNUVDUVBUUJYDUU
      LETVMVLUULWOWLZUVEUUDUUAUVHUVBXNUVDUUCUULWOWNETUULWOYDESVNVOVPVQVRYGYDWNE
      LZYDWOELZXQPZUUBUUFYGUUQUVIUVKRZUUSYGYLXIXJUUQUVLRYTYNUUTUUPUVLYDUUJELZYD
      UULELZUUNPZRUVIUVNUULWNELZPZRBCDYDWNWOGGGUUIYDWLZUUKUVMUUOUVOUUIYDUUJESUV
      RUUMUVNUUNUUIYDUULESVKVLUUJWNWLZUVMUVIUVOUVQUUJWNYDETUVSUUNUVPUVNUUJWNUUL
      ETVMVLUVHUVQUVKUVIUVHUVNUVJUVPXQUULWOYDETUULWOWNESVNVOVPVQVRWNYDEXMYSULUU
      FXQUVJPUVKXOXQUUEUVJXTWOYDEXKYSULUNXQUVJUOQVSWAWNYDEWTUPUUHXPUUCUUEPZPUUG
      XBXPYPUVTYAWOYDEWTUPUNXNXOUUCUUEWBQVSWCYKWNYDYBLYHYJMWNYDFYBHUSWNYDWRXAVA
      QVJAXIWNWNFLZAXIMZWNWNWRLZWNWNXALZNZUWAUWBXIXIUWCAXIUCZUWFWNWNGGXMOZVJUWB
      WNWNELZWNWNWTLZPZUWDUWBUWHNZUWINUWJNAGEWDXIUWKABCDGEIJWEGWNEWFWGZUWBUWHUW
      IUWLWNWNEXMXMULWHUWHUWIWIVJWNWNEWTUPWHUWAWNWNYBLUWCUWEMWNWNFYBHUSWNWNWRXA
      VAQZVJUWAXIAUWAUWCXIUWAUWCUWEUWMVCUWCXIXIUWGVCVEWJWKWM $.
      $( [9-Jul-2014] $)
  $}

  ${
    $d y A $.  $d v x B $.  $d v w z R $.  $d v w x y z $.
    eqer.1 $e |- ( x = y -> A = B ) $.
    eqer.2 $e |- R = { <. x , y >. | A = B } $.
    $( Lemma for ~ eqer . $)
    eqerlem $p |- ( z R w <-> [_ z / x ]_ A = [_ w / x ]_ A ) $=
      ( vv cv wceq wsbc csb cvv wcel wb vex ax-mp eqeq2i wbr brabsb ax-17 sbbii
      sbceq1g csbief bitr4i sbceq2g csbcog bitri 3bitri ) CKZDKZGUAEFLZAULMZBUM
      MAULENZABKZENZLZBUMMZUPAUMENZLZUNABCDGIUBUOUSBDUOUPFLZUSULOPUOVCQCRAULEFO
      UESURFUPAJUQEFBRJKFPAUCHUFTUGUDUTUPBUMURNZLZVBUMOPZUTVEQDRZBUMUPUROUHSVDV
      AUPVFVDVALVGABUMEOUISTUJUK $.
      $( [17-Mar-2008] $)

    $( Equivalence relation involving equality of dependent classes ` A ( x ) `
       and ` B ( y ) ` . $)
    eqer $p |- Er R $=
      ( vz vw vv cv csb wceq wbr id eqcomd eqerlem 3imtr4i wa eqtr anbi12i iser
      ) HIJEAHKZCLZAIKZCLZMZUFUDMUCUEENZUEUCENUGUDUFUGOPABHICDEFGQZABIHCDEFGQRU
      GUFAJKZCLZMZSUDUKMUHUEUJENZSUCUJENUDUFUKTUHUGUMULUIABIJCDEFGQUAABHJCDEFGQ
      RUB $.
      $( [17-Mar-2008] $)
  $}

  ${
    $d x y z $.
    $( The identity relation is an equivalence relation.  (The proof was
       shortened by Andrew Salmon, 22-Oct-2011.)  (The proof was shortened by
       Mario Carneiro, 9-Jul-2014.) $)
    ider $p |- Er _I $=
      ( vx vy cv cid wceq id dfid3 eqer ) ABACZBCZDIJEFABGH $.
      $( [9-Jul-2014] $) $( [10-May-1998] $)
  $}

  $( Equality theorem for equivalence class. $)
  eceq1 $p |- ( A = B -> [ A ] C = [ B ] C ) $=
    ( wceq csn cima cec sneq imaeq2d df-ec 3eqtr4g ) ABDZCAEZFCBEZFACGBCGLMNCAB
    HIACJBCJK $.
    $( [23-Jul-1995] $)

  $( Equality theorem for equivalence class. $)
  eceq2 $p |- ( A = B -> [ C ] A = [ C ] B ) $=
    ( wceq csn cima cec imaeq1 df-ec 3eqtr4g ) ABDACEZFBKFCAGCBGABKHCAICBIJ $.
    $( [23-Jul-1995] $)

  ${
    $d x A $.  $d x B $.  $d x R $.
    $( Membership in an equivalence class.  Theorem 72 of [Suppes] p. 82. $)
    elecg $p |- ( ( A e. V /\ B e. W ) -> ( A e. [ B ] R <-> B R A ) ) $=
      ( wcel wa csn cima cop cec wbr elimasng ancoms df-ec eleq2i df-br 3bitr4g
      wb ) ADFZBEFZGACBHIZFZBAJCFZABCKZFBACLUATUCUDSCBAEDMNUEUBABCOPBACQR $.
      $( [9-Jul-2014] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    elec.1 $e |- A e. _V $.
    elec.2 $e |- B e. _V $.
    $( Membership in an equivalence class.  Theorem 72 of [Suppes] p. 82. $)
    elec $p |- ( A e. [ B ] R <-> B R A ) $=
      ( cvv wcel cec wbr wb elecg mp2an ) AFGBFGABCHGBACIJDEABCFFKL $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y R $.
    ecss.1 $e |- ( ph -> Er R ) $.
    ecss.2 $e |- ( ph -> dom R = X ) $.
    $( An equivalence class is a subset of the domain. $)
    ecss $p |- ( ph -> [ A ] R C_ X ) $=
      ( crn cec csn cima df-ec imassrn eqsstri cdm wer wceq erdmrn syl syl5sseq
      eqtr3d ) ACGZBCHZDUBCBIZJUABCKCUCLMACNZUADACOUDUAPECQRFTS $.
      $( [9-Jul-2014] $) $( [6-Aug-1995] $)
  $}

  ${
    $d x R $.  $d x A $.
    $( A representative of a nonempty equivalence class belongs to the domain
       of the equivalence relation. $)
    ecdmn0 $p |- ( A e. dom R <-> [ A ] R =/= (/) ) $=
      ( vx cdm wcel cvv cec c0 wne elex cv wex n0 ecexr exlimiv sylbi wbr elecg
      wb vex mpan exbidv a1i eldmg 3bitr4rd pm5.21nii ) ABDZEZAFEZABGZHIZAUGJUK
      CKZUJEZCLZUICUJMZUMUICULABNOPUIUNAULBQZCLUKUHUIUMUPCULFEUIUMUPSCTULABFFRU
      AUBUKUNSUIUOUCCABFUDUEUF $.
      $( [9-Jul-2014] $) $( [15-Feb-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $d x ph $.
    erth.1 $e |- ( ph -> Er R ) $.
    erth.2 $e |- ( ph -> dom R = X ) $.
    erth.3 $e |- ( ph -> A e. X ) $.
    erth.4 $e |- ( ph -> B e. V ) $.
    $( Basic property of equivalence relations.  Theorem 73 of [Suppes] p. 82.
       (Revised by Mario Carneiro, 9-Jul-2014.) $)
    erth $p |- ( ph -> ( A R B <-> [ A ] R = [ B ] R ) ) $=
      ( vx wbr cec wa wcel cvv ertr wb adantr elecg wceq cv simpl ersymb biimpa
      jca vex a1i impl sylan impbida sylancr 3bitr4d eqrdv erref syl2anc mpbird
      wer simpr eleqtrd mpbid ersym ) ABCDLZBDMZCDMZUAZAVCNZKVDVEVGBKUBZDLZCVHD
      LZVHVDOZVHVEOZVGVIVJVGACBDLZNVIVJVGAVMAVCUCAVCVMABCDFEGIJUDUEUFAVMVIVJACB
      VHDEFPGJIVHPOZAKUGZUHZQUIUJAVCVJVIABCVHDFEPGIJVPQUIUKVGVNBFOZVKVIRVOAVQVC
      ISVHBDPFTULVGVNCEOZVLVJRVOAVRVCJSVHCDPETULUMUNAVFNZCBDEFADURVFGSAVRVFJSZA
      VQVFISZVSBVEOZVMVSBVDVEVSBVDOZBBDLZAWDVFABDFGHIUOSVSVQVQWCWDRWAWABBDFFTUP
      UQAVFUSUTVSVQVRWBVMRWAVTBCDFETUPVAVBUK $.
      $( [9-Jul-2014] $) $( [23-Jul-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $d x ph $.
    erth2.1 $e |- ( ph -> Er R ) $.
    erth2.2 $e |- ( ph -> dom R = X ) $.
    erth2.3 $e |- ( ph -> A e. V ) $.
    erth2.4 $e |- ( ph -> B e. X ) $.
    $( Basic property of equivalence relations.  Compare Theorem 73 of [Suppes]
       p. 82.  Assumes membership of the second argument in the domain. $)
    erth2 $p |- ( ph -> ( A R B <-> [ A ] R = [ B ] R ) ) $=
      ( wbr cec wceq ersymb erth eqcom syl6bb bitrd ) ABCDKCBDKZBDLZCDLZMZABCDE
      FGIJNASUATMUBACBDEFGHJIOUATPQR $.
      $( [9-Jul-2014] $) $( [30-Jul-1995] $)
  $}

  ${
    erthi.1 $e |- ( ph -> Er R ) $.
    erthi.2 $e |- ( ph -> A e. V ) $.
    erthi.3 $e |- ( ph -> B e. X ) $.
    erthi.4 $e |- ( ph -> A R B ) $.
    $( Basic property of equivalence relations.  Part of Lemma 3N of [Enderton]
       p. 57. $)
    erthi $p |- ( ph -> [ A ] R = [ B ] R ) $=
      ( wbr cec wceq cdm eqidd wcel breldmg syl2anc erth mpbid ) ABCDKZBDLCDLMJ
      ABCDFDNZGAUBOABEPUABUBPHJBCEDQRIST $.
      $( [9-Jul-2014] $) $( [30-Jul-1995] $)
  $}

  ${
    ereldm.1 $e |- ( ph -> Er R ) $.
    ereldm.2 $e |- ( ph -> dom R = X ) $.
    ereldm.3 $e |- ( ph -> [ A ] R = [ B ] R ) $.
    ${
      ereldm.4 $e |- ( ph -> A e. X ) $.
      $( Equality of equivalence classes implies equivalence of domain
         membership. $)
      ereldm $p |- ( ph -> B e. X ) $=
        ( cec ecss wcel wbr wceq cvv erref wb elecg syl2anc mpbird eleqtrd erth
        ecexr syl sseldd ) ABDJZECABDEFGKACUFLZBCDMZAUHUFCDJZNHABCDOEFGIABUILCO
        LZABUFUIABUFLZBBDMZABDEFGIPABELZUMUKULQIIBBDEERSTHUABCDUCUDZUBTAUJUMUGU
        HQUNICBDOERSTUE $.
        $( [9-Jul-2014] $) $( [28-Jan-1996] $)
    $}

    $( Equality of equivalence classes implies equivalence of domain
       membership. $)
    ereldmb $p |- ( ph -> ( A e. X <-> B e. X ) ) $=
      ( wcel wa wer adantr cdm wceq cec simpr ereldm eqcomd impbida ) ABEIZCEIZ
      ATJBCDEADKZTFLADMENZTGLABDOZCDOZNTHLATPQAUAJCBDEAUBUAFLAUCUAGLAUEUDNUAAUD
      UEHRLAUAPQS $.
      $( [9-Jul-2014] $) $( [28-Jan-1996] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.
    $( Equivalence classes do not overlap.  In other words, two equivalence
       classes are either equal or disjoint.  Theorem 74 of [Suppes] p. 83.
       (Revised by Mario Carneiro, 9-Jul-2014.) $)
    erdisj $p |- ( Er R ->
                   ( [ A ] R = [ B ] R \/ ( [ A ] R i^i [ B ] R ) = (/) ) ) $=
      ( vx wer cec cin c0 wceq wn wcel cvv adantl ecexr syl elecg sylancr mpbid
      wbr wb cv wex neq0 wa simpl elin simplbi simprbi vex ertr4d erthi exlimdv
      ex syl5bi orrd orcomd ) CEZACFZBCFZGZHIZURUSIZUQVAVBVAJDUAZUTKZDUBUQVBDUT
      UCUQVDVBDUQVDVBUQVDUDZABCLLUQVDUEZVEVCURKZALKZVDVGUQVDVGVCUSKZVCURUSUFZUG
      MZVCACNOZVEVIBLKZVDVIUQVDVGVIVJUHMZVCBCNOZVEAVCBCLURLVFVLVKVOVEVGAVCCSZVK
      VEVCLKZVHVGVPTDUIZVLVCACLLPQRVEVIBVCCSZVNVEVQVMVIVSTVRVOVCBCLLPQRUJUKUMUL
      UNUOUP $.
      $( [9-Jul-2014] $) $( [15-Jun-2004] $)
  $}

  $( An equivalence class modulo the identity relation is a singleton. $)
  ecidsn $p |- [ A ] _I = { A } $=
    ( cid cec csn cima df-ec imai eqtri ) ABCBADZEIABFIGH $.
    $( [24-Oct-2004] $)

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Equality theorem for quotient set. $)
    qseq1 $p |- ( A = B -> ( A /. C ) = ( B /. C ) ) $=
      ( vy vx wceq cv cec wrex cab cqs rexeq abbidv df-qs 3eqtr4g ) ABFZDGEGCHF
      ZEAIZDJQEBIZDJACKBCKPRSDQEABLMEDACNEDBCNO $.
      $( [23-Jul-1995] $)

    $( Equality theorem for quotient set. $)
    qseq2 $p |- ( A = B -> ( C /. A ) = ( C /. B ) ) $=
      ( vy vx wceq cec wrex cab cqs eceq2 eqeq2d rexbidv abbidv df-qs 3eqtr4g
      cv ) ABFZDQZEQZAGZFZECHZDISTBGZFZECHZDICAJCBJRUCUFDRUBUEECRUAUDSABTKLMNED
      CAOEDCBOP $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( Closed form of ~ elqs .  (Contributed by Rodolfo Medina,
       12-Oct-2010.) $)
    elqsg $p |- ( B e. V -> ( B e. ( A /. R ) <-> E. x e. A B = [ x ] R ) ) $=
      ( vy cv cec wceq wrex cqs eqeq1 rexbidv df-qs elab2g ) FGZAGDHZIZABJCQIZA
      BJFCBDKEPCIRSABPCQLMAFBDNO $.
      $( [12-Oct-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    elqs.1 $e |- B e. _V $.
    $( Membership in a quotient set. $)
    elqs $p |- ( B e. ( A /. R ) <-> E. x e. A B = [ x ] R ) $=
      ( cvv wcel cqs cv cec wceq wrex wb elqsg ax-mp ) CFGCBDHGCAIDJKABLMEABCDF
      NO $.
      $( [12-Nov-2008] $) $( [23-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    $( Membership in a quotient set. $)
    elqsi $p |- ( B e. ( A /. R ) -> E. x e. A B = [ x ] R ) $=
      ( cqs wcel cv cec wceq wrex elqsg ibi ) CBDEZFCAGDHIABJABCDMKL $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d R x $.  $d B x $.  $d A x $.
    $( Membership of an equivalence class in a quotient set.  (Contributed by
       Jeff Madsen, 10-Jun-2010.) $)
    ecelqsg $p |- ( ( R e. V /\ B e. A ) -> [ B ] R e. ( A /. R ) ) $=
      ( vx wcel cec cv wceq wrex cqs eceq1 eqeq2d rcla4ev mpan2 cvv ecexg elqsg
      eqid wb syl biimpar sylan2 ) BAFZCDFZBCGZEHZCGZIZEAJZUFACKFZUDUFUFIZUJUFS
      UIULEBAUGBIUHUFUFUGBCLMNOUEUKUJUEUFPFUKUJTBDCQEAUFCPRUAUBUC $.
      $( [9-Jul-2014] $) $( [10-Jun-2010] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y R $.
    ecelqsi.1 $e |- R e. _V $.
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsi $p |- ( B e. A -> [ B ] R e. ( A /. R ) ) $=
      ( cvv wcel cec cqs ecelqsg mpan ) CEFBAFBCGACHFDABCEIJ $.
      $( [9-Jul-2014] $) $( [25-Jul-1995] $)
  $}

  ${
    ecopqsi.1 $e |- R e. _V $.
    ecopqsi.2 $e |- S = ( ( A X. A ) /. R ) $.
    $( "Closure" law for equivalence class of ordered pairs. $)
    ecopqsi $p |- ( ( B e. A /\ C e. A ) -> [ <. B , C >. ] R e. S ) $=
      ( wcel wa cop cxp cec opelxpi cqs ecelqsi syl6eleqr syl ) BAHCAHIBCJZAAKZ
      HZRDLZEHBCAAMTUASDNESRDFOGPQ $.
      $( [25-Mar-1996] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    $( A quotient set exists.  (Contributed by FL, 19-May-2007.) $)
    qsexg $p |- ( A e. V -> ( A /. R ) e. _V ) $=
      ( vy vx wcel cqs cv cec wceq wrex cab cvv df-qs abrexexg syl5eqel ) ACFAB
      GDHEHBIZJEAKDLMEDABNEDAQCOP $.
      $( [9-Jul-2014] $) $( [23-Apr-2007] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    qsex.1 $e |- A e. _V $.
    $( A quotient set exists. $)
    qsex $p |- ( A /. R ) e. _V $=
      ( cvv wcel cqs qsexg ax-mp ) ADEABFDECABDGH $.
      $( [14-Aug-1995] $)
  $}

  ${
    $d x y A $.  $d x y R $.  $d x V $.
    $( The union of a quotient set. $)
    uniqs $p |- ( R e. V -> U. ( A /. R ) = ( R " A ) ) $=
      ( vy vx wcel cv cec wceq wrex cab cuni ciun cqs cima wral ecexg ralrimivw
      cvv dfiun2g syl eqcomd df-qs unieqi csn df-ec a1i iuneq2i imaiun 3eqtr2ri
      iunid imaeq2i 3eqtr4g ) BCFZDGEGZBHZIEAJDKZLZEAUPMZABNZLBAOZUNUSURUNUPSFZ
      EAPUSURIUNVBEAUOCBQREDAUPSTUAUBUTUQEDABUCUDUSEABUOUEZOZMBEAVCMZOVAEAUPVDU
      PVDIUOAFUOBUFUGUHEBAVCUIVEABEAUKULUJUM $.
      $( [9-Dec-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z R $.
    qsss.1 $e |- ( ph -> Er R ) $.
    qsss.2 $e |- ( ph -> dom R = A ) $.
    qsss.3 $e |- ( ph -> R e. V ) $.
    $( The union of a quotient set. $)
    uniqs2 $p |- ( ph -> U. ( A /. R ) = A ) $=
      ( cdm cima crn cqs cuni imadmrn wcel wceq uniqs syl imaeq2d eqtr4d wer
      erdmrn eqtr3d 3eqtr4a ) ACCHZIZCJZBCKLZBCMAUGCBIZUEACDNUGUHOGBCDPQAUDBCFR
      SAUDBUFFACTUDUFOECUAQUBUC $.
      $( [11-Jul-2014] $)

    $( A quotient set is a set of subsets of the base set.  (Contributed by
       Mario Carneiro, 9-Jul-2014.) $)
    qsss $p |- ( ph -> ( A /. R ) C_ ~P A ) $=
      ( cqs cuni wss cpw wceq uniqs2 eqimss syl sspwuni sylibr ) ABCHZIZBJZRBKJ
      ASBLTABCDEFGMSBNORBPQ $.
      $( [9-Jul-2014] $)
  $}

  ${
    $d x y A $.  $d x y R $.
    snec.1 $e |- A e. _V $.
    $( The singleton of an equivalence class. $)
    snec $p |- { [ A ] R } = ( { A } /. R ) $=
      ( vy vx cv cec wceq csn wrex cab cqs eceq1 eqeq2d rexsn abbii df-qs df-sn
      3eqtr4ri ) DFZEFZBGZHZEAIZJZDKTABGZHZDKUDBLUFIUEUGDUCUGEACUAAHUBUFTUAABMN
      OPEDUDBQDUFRS $.
      $( [9-Jul-2014] $) $( [29-Jan-1999] $)
  $}

  ${
    ecqs.1 $e |- R e. _V $.
    $( Equivalence class in terms of quotient set. $)
    ecqs $p |- [ A ] R = U. ( { A } /. R ) $=
      ( cec csn cima cqs cuni df-ec cvv wcel wceq uniqs ax-mp eqtr4i ) ABDBAEZF
      ZPBGHZABIBJKRQLCPBJMNO $.
      $( [15-Jan-2009] $) $( [29-Jan-1999] $)
  $}

  ${
    $d y A $.
    ecid.1 $e |- A e. _V $.
    $( A set is equal to its converse epsilon coset.  (Note: converse epsilon
       is not an equivalence relation.) $)
    ecid $p |- [ A ] `' _E = A $=
      ( vy cep ccnv cec cv wcel wbr vex elec brcnv epelc 3bitri eqriv ) CADEZFZ
      ACGZQHARPIRADIRAHRAPCJZBKARDBSLRASBMNO $.
      $( [9-Jul-2014] $) $( [13-Aug-1995] $)
  $}

  ${
    $d x y A $.
    $( A set is equal to its quotient set mod converse epsilon.  (Note:
       converse epsilon is not an equivalence relation.) $)
    qsid $p |- ( A /. `' _E ) = A $=
      ( vy vx cep ccnv cqs cv cec wceq wrex wcel ecid eqeq2i eqcom bitri rexbii
      vex elqs risset 3bitr4i eqriv ) BADEZFZABGZCGZUBHZIZCAJUEUDIZCAJUDUCKUDAK
      UGUHCAUGUDUEIUHUFUEUDUECQLMUDUENOPCAUDUBBQRCUDASTUA $.
      $( [9-Jul-2014] $) $( [13-Aug-1995] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x R $.  $d x ps $.  $d x ch $.
    ectocl.1 $e |- S = ( B /. R ) $.
    ectocl.2 $e |- ( [ x ] R = A -> ( ph <-> ps ) ) $.
    ${
      ectocld.3 $e |- ( ( ch /\ x e. B ) -> ph ) $.
      $( Implicit substitution of class for equivalence class. $)
      ectocld $p |- ( ( ch /\ A e. S ) -> ps ) $=
        ( wcel cv cec wceq wrex cqs elqsi eleq2s wa wb syl5ibcom rexlimdva syl5
        eqcoms imp ) CEHLZBUGEDMZGNZOZDFPZCBUKEFGQHDFEGRISCUJBDFCUHFLTAUJBKABUA
        UIEJUEUBUCUDUF $.
        $( [9-Jul-2014] $)
    $}

    ectocl.3 $e |- ( x e. B -> ph ) $.
    $( Implicit substitution of class for equivalence class. $)
    ectocl $p |- ( A e. S -> ps ) $=
      ( wtru wcel tru cv adantl ectocld mpan ) KDGLBMABKCDEFGHICNELAKJOPQ $.
      $( [9-Jul-2014] $) $( [23-Jul-1995] $)
  $}

  ${
    $d x R $.  $d x A $.  $d x B $.
    $( A quotient set doesn't contain the empty set. $)
    elqsn0 $p |- ( ( dom R = A /\ B e. ( A /. R ) ) -> B =/= (/) ) $=
      ( vx cv cec c0 wne cdm wceq eqid neeq1 wcel wa eleq2 biimpar ecdmn0 sylib
      cqs ectocld ) DEZCFZGHZBGHCIZAJZDBACACSZUFKUBBGLUEUAAMZNUAUDMZUCUEUHUGUDA
      UAOPUACQRT $.
      $( [21-Mar-2007] $) $( [24-Aug-1995] $)
  $}

  ${
    $( Membership of an equivalence class in a quotient set. $)
    ecelqsdm $p |- ( ( dom R = A /\ [ B ] R e. ( A /. R ) ) -> B e. A ) $=
      ( cdm wceq cec cqs wcel wa c0 wne elqsn0 ecdmn0 sylibr simpl eleqtrd ) CD
      ZAEZBCFZACGHZIZBQAUASJKBQHASCLBCMNRTOP $.
      $( [21-Mar-2007] $) $( [30-Jul-1995] $)
  $}

  ${
    $d x y A $.  $d x B $.  $d x y C $.  $d x y ph $.  $d x y R $.
    qsdisj.1 $e |- ( ph -> Er R ) $.
    qsdisj.2 $e |- ( ph -> B e. ( A /. R ) ) $.
    qsdisj.3 $e |- ( ph -> C e. ( A /. R ) ) $.
    $( Members of a quotient set do not overlap.  (Contributed by Rodolfo
       Medina, 12-Oct-2010.)  (Revised by Mario Carneiro, 11-Jul-2014.) $)
    qsdisj $p |- ( ph -> ( B = C \/ ( B i^i C ) = (/) ) ) $=
      ( vx vy wcel wceq cin c0 wo cv cec eqeq1d orbi12d wa cqs eqid eqeq1 ineq1
      adantr eqeq2 ineq2 wer ad2antrr erdisj syl ectocld mpdan ) ACBEUAZKCDLZCD
      MZNLZOZGIPZEQZDLZUTDMZNLZOZURAICBEUNUNUBZUTCLZVAUOVCUQUTCDUCVFVBUPNUTCDUD
      RSAUSBKZTZDUNKZVDAVIVGHUEUTJPZEQZLZUTVKMZNLZOZVDVHJDBEUNVEVKDLZVLVAVNVCVK
      DUTUFVPVMVBNVKDUTUGRSVHVJBKZTEUHZVOAVRVGVQFUIUSVJEUJUKULUMULUM $.
      $( [11-Jul-2014] $) $( [12-Oct-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z ps $.
    ecoptocl.1 $e |- S = ( ( B X. C ) /. R ) $.
    ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    ecoptocl.3 $e |- ( ( x e. B /\ y e. C ) -> ph ) $.
    $( Implicit substitution of class for equivalence class of ordered pair. $)
    ecoptocl $p |- ( A e. S -> ps ) $=
      ( vz cxp cqs wcel cv cec wceq wi wrex elqsi cop eceq1 eqeq2d imbi1d wa wb
      eqid eqcoms syl5ibcom optocl rexlimiv syl eleq2s ) BEFGNZHOZIEUQPEMQZHRZS
      ZMUPUABMUPEHUBUTBMUPECQZDQZUCZHRZSZBTUTBTCDURFGUPUPUIVCURSZVEUTBVFVDUSEVC
      URHUDUEUFVAFPVBGPUGAVEBLABUHVDEKUJUKULUMUNJUO $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w C $.  $d x y z w D $.  $d z w S $.
    $d x y z w R $.  $d x y ps $.  $d z w ch $.
    2ecoptocl.1 $e |- S = ( ( C X. D ) /. R ) $.
    2ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    2ecoptocl.3 $e |- ( [ <. z , w >. ] R = B -> ( ps <-> ch ) ) $.
    2ecoptocl.4 $e |- ( ( ( x e. C /\ y e. D ) /\
                      ( z e. C /\ w e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    2ecoptocl $p |- ( ( A e. S /\ B e. S ) -> ch ) $=
      ( wcel wi cv cop cec wceq imbi2d wa ex ecoptocl com12 impcom ) IMRHMRZCUJ
      BSUJCSFGIJKLMNFTZGTZUALUBIUCBCUJPUDUJUKJRULKRUEZBUMASUMBSDEHJKLMNDTZETZUA
      LUBHUCABUMOUDUNJRUOKRUEUMAQUFUGUHUGUI $.
      $( [23-Jul-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d v u C $.  $d x y z w v u D $.
    $d z w v u S $.  $d x y z w v u R $.  $d x y ps $.  $d z w ch $.
    $d v u th $.
    3ecoptocl.1 $e |- S = ( ( D X. D ) /. R ) $.
    3ecoptocl.2 $e |- ( [ <. x , y >. ] R = A -> ( ph <-> ps ) ) $.
    3ecoptocl.3 $e |- ( [ <. z , w >. ] R = B -> ( ps <-> ch ) ) $.
    3ecoptocl.4 $e |- ( [ <. v , u >. ] R = C -> ( ch <-> th ) ) $.
    3ecoptocl.5 $e |- ( ( ( x e. D /\ y e. D ) /\
                      ( z e. D /\ w e. D ) /\ ( v e. D /\ u e. D ) ) -> ph ) $.
    $( Implicit substitution of classes for equivalence classes of ordered
       pairs. $)
    3ecoptocl $p |- ( ( A e. S /\ B e. S /\ C e. S ) -> th ) $=
      ( wcel wa wi cop cec wceq imbi2d 3expib ecoptocl com12 2ecoptocl 3impib
      cv ) KPUBZLPUBZMPUBZDUPUQUCUODUOBUDUOCUDUODUDGHIJLMNNOPQGUNZHUNZUEOUFLUGB
      CUOSUHIUNZJUNZUEOUFMUGCDUOTUHUOURNUBUSNUBUCZUTNUBVANUBUCZUCZBVDAUDVDBUDEF
      KNNOPQEUNZFUNZUEOUFKUGABVDRUHVENUBVFNUBUCVBVCAUAUIUJUKULUKUM $.
      $( [9-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
    $d x y z w v u D $.  $d x y z w v u S $.  $d x y H $.  $d z w v u G $.
    $d x y ph $.  $d z w v u ps $.
    brecop.1 $e |- S e. _V $.
    brecop.2 $e |- Er S $.
    brecop.3 $e |- dom S = ( G X. G ) $.
    brecop.4 $e |- H = ( ( G X. G ) /. S ) $.
    brecop.5 $e |- R = { <. x , y >. | ( ( x e. H /\ y e. H ) /\
                      E. z E. w E. v E. u ( ( x = [ <. z , w >. ] S /\
                      y = [ <. v , u >. ] S ) /\ ph ) ) } $.
    brecop.6 $e |- ( ( ( ( z e. G /\ w e. G ) /\ ( A e. G /\ B e. G ) ) /\
                       ( ( v e. G /\ u e. G ) /\ ( C e. G /\ D e. G ) ) ) ->
                     ( ( [ <. z , w >. ] S = [ <. A , B >. ] S /\
                         [ <. v , u >. ] S = [ <. C , D >. ] S ) ->
                     ( ph <-> ps ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number
       construction. $)
    brecop $p |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                ( [ <. A , B >. ] S R [ <. C , D >. ] S <-> ps ) ) $=
      ( wcel wa cop cec wbr cv wceq wex ecopqsi copab df-br eleq2i bitri anbi1d
      wb eqeq1 4exbidv anbi2d opelopab2 syl5bb syl2an wi opeq12 syl anim12i cxp
      eceq1 opelxpi vex opelxp wer a1i id ereldmb syl5bbr syl5ibr im2anan9 an4s
      cdm com13 mpdd pm5.74d cgsex4g eqcom anbi12i biimt anbi12d 3bitr4d bitrd
      ex ) IOUCJOUCUDZKOUCLOUCUDZUDZIJUEZNUFZKLUEZNUFZMUGZWQEUHZFUHZUEZNUFZUIZW
      SGUHZHUHZUEZNUFZUIZUDZAUDZHUJGUJFUJEUJZBWMWQPUCZWSPUCZWTXMUQWNOIJNPQTUKOK
      LNPQTUKWTWQWSUEZCUHZPUCDUHZPUCUDXQXDUIZXRXIUIZUDZAUDZHUJGUJFUJEUJZUDCDULZ
      UCZXNXOUDXMWTXPMUCYEWQWSMUMMYDXPUAUNUOYCXEXTUDZAUDZHUJGUJFUJEUJXMCDWQWSPP
      XQWQUIZYBYGEFGHYHYAYFAYHXSXEXTXQWQXDURUPUPUSXRWSUIZYGXLEFGHYIYFXKAYIXTXJX
      EXRWSXIURUTUPUSVAVBVCWOXDWQUIZXIWSUIZUDZWOAVDZUDZHUJGUJFUJEUJWOBVDZXMBYMY
      OYLEFGHIJKLOOXAIUIXBJUIUDZYJXFKUIXGLUIUDZYKYPXCWPUIYJXAXBIJVEXCWPNVIVFYQX
      HWRUIYKXFXGKLVEXHWRNVIVFVGYLWOABYLWOXAOUCXBOUCUDZXFOUCXGOUCUDZUDZABUQZYJW
      MYRYKWNYSWMYRYJWPOOVHZUCZIJOOVJYRXCUUBUCYJUUCXAXBOOFVKVLYJXCWPNUUBNVMZYJR
      VNNWAUUBUIZYJSVNYJVOVPVQVRWNYSYKWRUUBUCZKLOOVJYSXHUUBUCYKUUFXFXGOOHVKVLYK
      XHWRNUUBUUDYKRVNUUEYKSVNYKVOVPVQVRVSYTWOYLUUAYTWOYLUUAVDZYRWMYSWNUUGUBVTW
      LWBWCWDWEWOXLYNEFGHWOXKYLAYMXKYLUQWOXEYJXJYKWQXDWFWSXIWFWGVNWOAWHWIUSWOBW
      HWJWK $.
      $( [29-Jan-1996] $)
  $}

  ${
    brecop2.1 $e |- S e. _V $.
    brecop2.2 $e |- B e. _V $.
    brecop2.3 $e |- C e. _V $.
    brecop2.4 $e |- D e. _V $.
    brecop2.5 $e |- dom S = ( G X. G ) $.
    brecop2.6 $e |- H = ( ( G X. G ) /. S ) $.
    brecop2.7 $e |- R C_ ( H X. H ) $.
    brecop2.8 $e |- Q C_ ( G X. G ) $.
    brecop2.9 $e |- -. (/) e. G $.
    brecop2.10 $e |- dom F = ( G X. G ) $.
    brecop2.11 $e |- ( ( ( A e. G /\ B e. G ) /\ ( C e. G /\ D e. G ) ) ->
                    ( [ <. A , B >. ] S R [ <. C , D >. ] S <->
                    ( A F D ) Q ( B F C ) ) ) $.
    $( Binary relation on a quotient set.  Lemma for real number construction.
       Eliminates antecedent from last hypothesis. $)
    brecop2 $p |- ( [ <. A , B >. ] S R [ <. C , D >. ] S <->
                    ( A F D ) Q ( B F C ) ) $=
      ( cop cec wbr wcel wa cvv ecexg ax-mp brel cxp cqs cdm wceq ecelqsdm mpan
      co eleq2s opelxp sylib anim12i syl ovex ndmovrcl an42 pm5.21nii ) ABUBZGU
      CZCDUBZGUCZFUDZAIUEZBIUEZUFZCIUEZDIUEZUFZUFZADHUQZBCHUQZEUDZVKVHJUEZVJJUE
      ZUFVRVHVJJJFGUGUEVJUGUEKVIUGGUHUIQUJWBVNWCVQWBVGIIUKZUEZVNWEVHWDGULZJGUMW
      DUNZVHWFUEWEOWDVGGUOUPPURABIILUSUTWCVIWDUEZVQWHVJWFJWGVJWFUEWHOWDVIGUOUPP
      URCDIINUSUTVAVBWAVLVPUFZVMVOUFZUFZVRWAVSIUEZVTIUEZUFWKVSVTIIEBCHVCRUJWLWI
      WMWJADIHNTSVDBCIHMTSVDVAVBVLVPVMVOVEUTUAVF $.
      $( [21-Mar-2007] $) $( [13-Feb-1996] $)
  $}

  ${
    $d A p q x $.  $d B p q y $.  $d R p q x $.  $d S p q y $.  $d x y z $.
    erovlem.1 $e |- J = ( A /. R ) $.
    erovlem.2 $e |- K = ( B /. S ) $.
    erovlem.3 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) } $.
    $( Lemma for ~ erov and ~ eroprf . $)
    erovlem $p |- G = { <. <. x , y >. , z >. | ( ( x e. J /\ y e. K ) /\
                          E. p e. A E. q e. B ( ( x = [ p ] R /\ y = [ q ] S )
                                          /\ z = [ ( p F q ) ] T ) ) } $=
      ( cv wa wrex cec wceq co copab2 wcel simpl reximi cqs eleq2i df-qs abeq2i
      bitri anbi12i reeanv bitr4i sylibr pm4.71ri oprabbii eqtri ) JARZNRZFUAUB
      ZBRZMRZGUAUBZSZCRVAVDIUCHUAUBZSZMETZNDTZABCUDUTKUEZVCLUEZSZVJSZABCUDQVJVN
      ABCVJVMVJVFMETZNDTZVMVIVONDVHVFMEVFVGUFUGUGVMVBNDTZVEMETZSVPVKVQVLVRVKUTD
      FUHZUEVQKVSUTOUIVQAVSNADFUJUKULVLVCEGUHZUEVRLVTVCPUIVRBVTMBEGUJUKULUMVBVE
      NMDEUNUOUPUQURUS $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A p q r s t u x w z $.  $d B p q r s t u y w z $.
    $d R p q r s t u x w z $.  $d S p q r s t u y w z $.
    $d T p q r s t u w z $.  $d F p q r s t u w z $.  $d ph p q r s t u w z $.
    $d J p q w z $.  $d K p q w z $.
    eropr.1 $e |- J = ( A /. R ) $.
    eropr.2 $e |- K = ( B /. S ) $.
    eropr.3 $e |- ( ph -> T e. Z ) $.
    eropr.4 $e |- ( ph -> Er R ) $.
    eropr.5 $e |- ( ph -> Er S ) $.
    eropr.6 $e |- ( ph -> Er T ) $.
    eropr.7 $e |- ( ph -> A C_ dom R ) $.
    eropr.8 $e |- ( ph -> B C_ dom S ) $.
    eropr.9 $e |- ( ph -> C C_ dom T ) $.
    eropr.10 $e |- ( ph -> F : ( A X. B ) --> C ) $.
    eropr.11 $e |- ( ( ph /\ ( ( r e. A /\ s e. A ) /\ ( t e. B /\ u e. B ) ) )
                        -> ( ( r R s /\ t S u ) -> ( r F t ) T ( s F u ) ) ) $.
    $( Lemma for ~ erov and ~ eroprf . $)
    eroveu $p |- ( ( ph /\ ( x e. J /\ y e. K ) ) -> E! z E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) ) $=
      ( vw cv wcel wa cec wceq co wrex wex wi wal weu eleq2i df-qs abeq2i bitri
      cqs anbi12i biimpi adantl reeanv sylibr cvv adantr ecexg elisset biantrud
      3syl 2rexbidv mpbid 19.42v bicomi rexbii rexcom4 sylib eceq1 eqeq2d oveq1
      anbi1d syl anbi12d anbi2d oveq2 cbvrex2v bitr4i wbr cdm wer eqidd simprll
      wss sseldd simprlr erth simprrl simprrr cxp wf fovrn syl3anc ovex 3imtr3d
      eqeq2 biimprcd syl6 imp3a wb eqeq1 bi2anan9 imbi12d syl5ibrcom rexlimdvva
      a1i anassrs syl5bir alrimivv eu4 sylanbrc ) ABUMZNUNZCUMZOUNZUOZUOZYJTUMZ
      JUPZUQZYLSUMZKUPZUQZUOZDUMZYPYSMURZLUPZUQZUOZSHUSZTGUSZDUTZUUIUUBULUMZUUE
      UQZUOZSHUSTGUSZUOZUUCUUKUQZVAZULVBDVBUUIDVCYOUUBUUFDUTZUOZSHUSZTGUSZUUJYO
      UUBSHUSTGUSZUVAYOYRTGUSZUUASHUSZUOZUVBYNUVEAYNUVEYKUVCYMUVDYKYJGJVHZUNUVC
      NUVFYJUAVDUVCBUVFTBGJVEVFVGYMYLHKVHZUNUVDOUVGYLUBVDUVDCUVGSCHKVEVFVGVIVJV
      KYRUUATSGHVLVMYOUUBUUSTSGHYOUURUUBYOLPUNZUUEVNUNUURAUVHYNUCVOUUDPLVPDUUEV
      NVQVSVRVTWAUVAUUHDUTZTGUSUUJUUTUVITGUUTUUGDUTZSHUSUVIUUSUVJSHUVJUUSUUBUUF
      DWBWCWDUUGSDHWEVGWDUUHTDGWEVGWFYOUUQDULAUUQYNUUOYJRUMZJUPZUQZYLFUMZKUPZUQ
      ZUOZUUCUVKUVNMURZLUPZUQZUOZFHUSZYJQUMZJUPZUQZYLEUMZKUPZUQZUOZUUKUWCUWFMUR
      ZLUPZUQZUOZEHUSZUOZQGUSRGUSZAUUPUWPUWBRGUSZUWNQGUSZUOUUOUWBUWNRQGGVLUUIUW
      QUUNUWRUUGUWAUVMUUAUOZUUCUVKYSMURZLUPZUQZUOTSRFGHYPUVKUQZUUBUWSUUFUXBUXCY
      RUVMUUAUXCYQUVLYJYPUVKJWGWHWJUXCUUEUXAUUCUXCUUDUWTUQUUEUXAUQYPUVKYSMWIUUD
      UWTLWGWKWHWLYSUVNUQZUWSUVQUXBUVTUXDUUAUVPUVMUXDYTUVOYLYSUVNKWGWHWMUXDUXAU
      VSUUCUXDUWTUVRUQUXAUVSUQYSUVNUVKMWNUWTUVRLWGWKWHWLWOUUMUWMUWEUUAUOZUUKUWC
      YSMURZLUPZUQZUOTSQEGHYPUWCUQZUUBUXEUULUXHUXIYRUWEUUAUXIYQUWDYJYPUWCJWGWHW
      JUXIUUEUXGUUKUXIUUDUXFUQUUEUXGUQYPUWCYSMWIUUDUXFLWGWKWHWLYSUWFUQZUXEUWIUX
      HUWLUXJUUAUWHUWEUXJYTUWGYLYSUWFKWGWHWMUXJUXGUWKUUKUXJUXFUWJUQUXGUWKUQYSUW
      FUWCMWNUXFUWJLWGWKWHWLWOVIWPAUWOUUPRQGGUWOUWAUWMUOZEHUSFHUSAUVKGUNZUWCGUN
      ZUOZUOZUUPUWAUWMFEHHVLUXOUXKUUPFEHHAUXNUVNHUNZUWFHUNZUOZUXKUUPVAAUXNUXRUO
      ZUOZUWAUWMUUPUXTUWMUUPVAUWAUVLUWDUQZUVOUWGUQZUOZUWLUOZUVSUUKUQZVAUXTUYCUW
      LUYEUXTUYCUVSUWKUQZUWLUYEVAUXTUVKUWCJWQZUVNUWFKWQZUOUVRUWJLWQUYCUYFUKUXTU
      YGUYAUYHUYBUXTUVKUWCJGJWRZAJWSUXSUDVOUXTUYIWTUXTGUYIUVKAGUYIXBUXSUGVOAUXL
      UXMUXRXAZXCAUXLUXMUXRXDXEUXTUVNUWFKHKWRZAKWSUXSUEVOUXTUYKWTUXTHUYKUVNAHUY
      KXBUXSUHVOAUXNUXPUXQXFZXCAUXNUXPUXQXGXEWLUXTUVRUWJLVNLWRZALWSUXSUFVOUXTUY
      MWTUXTIUYMUVRAIUYMXBUXSUIVOUXTGHXHIMXIZUXLUXPUVRIUNAUYNUXSUJVOUYJUYLUVKUV
      NIGHMXJXKXCUWJVNUNUXTUWCUWFMXLYDXEXMUWLUYEUYFUUKUWKUVSXNXOXPXQUWAUWMUYDUU
      PUYEUVQUWMUYDXRUVTUVQUWIUYCUWLUVMUWEUYAUVPUWHUYBYJUVLUWDXSYLUVOUWGXSXTWJV
      OUVTUUPUYEXRUVQUUCUVSUUKXSVKYAYBXQYEYCYFYCYFVOYGUUIUUNDULUUPUUGUUMTSGHUUP
      UUFUULUUBUUCUUKUUEXSWMVTYHYI $.
      $( [9-Jul-2014] $) $( [10-Jun-2010] $)

    $d P x y z p q $.  $d Q x y z p q $.  $d A y $.  $d B x $.  $d R y $.
    $d S x $.  $d T x y $.  $d F x y $.  $d ph x y $.  $d J x y $.  $d K x y $.
    eropr.12 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. B
                ( ( x = [ p ] R /\ y = [ q ] S ) /\ z = [ ( p F q ) ] T ) } $.
    eropr.13 $e |- ( ph -> R e. X ) $.
    eropr.14 $e |- ( ph -> S e. Y ) $.
    $( The value of an operation defined on equivalence classes.  (Contributed
       by Jeff Madsen, 10-Jun-2010.) $)
    erov $p |- ( ( ph /\ P e. A /\ Q e. B )
                              -> ( [ P ] R G [ Q ] S ) = [ ( P F Q ) ] T ) $=
      ( wcel w3a cec co wceq cv wa wrex eqid pm3.2i eceq1 eqeq2d anbi1d anbi12d
      oveq1 syl anbi2d oveq2 rcla42ev mp3an3 3adant1 cvv wb simp1 cqs syl6eleqr
      ecelqsg sylan 3adant3 3adant2 ecexg 3ad2ant1 2rexbidv eroveu ovg syl13anc
      eqeq1 erovlem mpbird ) AJGUTZKHUTZVAZJLVBZKMVBZPVCJKOVCZNVBZVDZXBUEVEZLVB
      ZVDZXCUDVEZMVBZVDZVFZXEXGXJOVCZNVBZVDZVFZUDHVGUEGVGZWSWTXRAWSWTXBXBVDZXCX
      CVDZVFZXEXEVDZVFZXRYAYBXSXTXBVHXCVHVIXEVHVIXQYCXSXLVFZXEJXJOVCZNVBZVDZVFU
      EUDJKGHXGJVDZXMYDXPYGYHXIXSXLYHXHXBXBXGJLVJVKVLYHXOYFXEYHXNYEVDXOYFVDXGJX
      JOVNXNYENVJVOVKVMXJKVDZYDYAYGYBYIXLXTXSYIXKXCXCXJKMVJVKVPYIYFXEXEYIYEXDVD
      YFXEVDXJKJOVQYEXDNVJVOVKVMVRVSVTXAAXBQUTZXCRUTZXEWAUTZXFXRWBAWSWTWCAWSYJW
      TALSUTZWSYJURYMWSVFXBGLWDQGJLSWFUFWEWGWHAWTYKWSAMTUTZWTYKUSYNWTVFXCHMWDRH
      KMTWFUGWEWGWIAWSYLWTANUAUTYLUHXDUANWJVOWKBVEZXHVDZCVEZXKVDZVFZDVEZXOVDZVF
      ZUDHVGUEGVGXIYRVFZUUAVFZUDHVGUEGVGXMUUAVFZUDHVGUEGVGXRABCDXBXCXEWAQRPYOXB
      VDZUUBUUDUEUDGHUUFYSUUCUUAUUFYPXIYRYOXBXHWPVLVLWLYQXCVDZUUDUUEUEUDGHUUGUU
      CXMUUAUUGYRXLXIYQXCXKWPVPVLWLYTXEVDZUUEXQUEUDGHUUHUUAXPXMYTXEXOWPVPWLABCD
      EFGHILMNOQRUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPWMBCDGHLMNOPQRUDUEUFUGUQWQWNWO
      WR $.
      $( [10-Jun-2010] $)

    $d A m n $.  $d B m n $.  $d R m n $.  $d S m n $.  $d J j k $.
    $d K j k $.  $d L j k m n $.  $d G j k m n $.  $d ph j k m n $.
    $d m n p q x y z $.
    eropr.15 $e |- L = ( C /. T ) $.
    $( Functionality of an operation defined on equivalence classes.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    eroprf $p |- ( ph -> G : ( J X. K ) --> L ) $=
      ( vj vk vm vn cxp wfn cv co wcel wral wf wa cec wceq wrex copab2 copab wi
      weu wal eroveu ex alrimivv fnoprabg syl erovlem fneq1i df-xp fneq2i bitri
      sylibr cqs eleq2i vex elqs anbi12i reeanv erov 3expb fovrn ecelqsg syldan
      sylan syl6eleqr eqeltrd oveq12 eleq1d syl5ibrcom syl5bir syl5bi ralrimivv
      rexlimdvva ffnov sylanbrc ) ANOPVDZVEZUTVFZVAVFZNVGZQVHZVAPVIUTOVIXNQNVJA
      BVFZOVHCVFZPVHVKZXTUDVFZJVLVMYAUCVFZKVLVMVKDVFYCYDMVGLVLVMVKUCHVNUDGVNZVK
      BCDVOZYBBCVPZVEZXOAYBYEDVRZVQZCVSBVSYHAYJBCAYBYIABCDEFGHIJKLMOPTUAUBUCUDU
      EUFUGUHUIUJUKULUMUNUOVTWAWBYBYEBCDWCWDXOYFXNVEYHXNNYFBCDGHJKLMNOPUCUDUEUF
      UPWEWFXNYGYFBCOPWGWHWIWJAXSUTVAOPXPOVHZXQPVHZVKXPVBVFZJVLZVMZVBGVNZXQVCVF
      ZKVLZVMZVCHVNZVKZAXSYKYPYLYTYKXPGJWKZVHYPOUUBXPUEWLVBGXPJUTWMWNWIYLXQHKWK
      ZVHYTPUUCXQUFWLVCHXQKVAWMWNWIWOUUAYOYSVKZVCHVNVBGVNAXSYOYSVBVCGHWPAUUDXSV
      BVCGHAYMGVHZYQHVHZVKZVKZXSUUDYNYRNVGZQVHUUHUUIYMYQMVGZLVLZQAUUEUUFUUIUUKV
      MABCDEFGHIYMYQJKLMNOPRSTUAUBUCUDUEUFUGUHUIUJUKULUMUNUOUPUQURWQWRUUHUUKILW
      KZQAUUGUUJIVHZUUKUULVHZAGHVDIMVJZUUGUUMUNUUOUUEUUFUUMYMYQIGHMWSWRXBALTVHU
      UMUUNUGIUUJLTWTXBXAUSXCXDUUDXRUUIQXPYNXQYRNXEXFXGXKXHXIXJUTVAOPQNXLXM $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d A p q r s t u x y z $.  $d R p q r s t u x y z $.
    $d F p q r s t u x y z $.  $d ph p q r s t u x y z $.  $d J p q x y z $.
    $d P p q x y z $.  $d Q p q x y z $.
    eropr2.1 $e |- J = ( A /. R ) $.
    eropr2.2 $e |- G = { <. <. x , y >. , z >. | E. p e. A E. q e. A
                ( ( x = [ p ] R /\ y = [ q ] R ) /\ z = [ ( p F q ) ] R ) } $.
    eropr2.3 $e |- ( ph -> R e. X ) $.
    eropr2.4 $e |- ( ph -> Er R ) $.
    eropr2.5 $e |- ( ph -> A C_ dom R ) $.
    eropr2.6 $e |- ( ph -> F : ( A X. A ) --> A ) $.
    eropr2.7 $e |- ( ( ph /\ ( ( r e. A /\ s e. A ) /\ ( t e. A /\ u e. A ) ) )
                        -> ( ( r R s /\ t R u ) -> ( r F t ) R ( s F u ) ) ) $.
    $( The value of an operation defined on equivalence classes.  (Contributed
       by Jeff Madsen, 10-Jun-2010.) $)
    erov2 $p |- ( ( ph /\ P e. A /\ Q e. A )
                              -> ( [ P ] R G [ Q ] R ) = [ ( P F Q ) ] R ) $=
      ( erov ) ABCDEFGGGHIJJJKLMMNNNOPQRSSUAUBUBUBUCUCUCUDUETUAUAUF $.
      $( [10-Jun-2010] $)

    $( Functionality of an operation defined on equivalence classes.
       (Contributed by Jeff Madsen, 10-Jun-2010.) $)
    eroprf2 $p |- ( ph -> G : ( J X. J ) --> J ) $=
      ( eroprf ) ABCDEFGGGHHHIJKKKLLLMNOPQQSTTTUAUAUAUBUCRSSQUD $.
      $( [10-Jun-2010] $)
  $}

  ${
    $d f g h t s r A $.  $d f g h t s r B $.  $d f g h t s r C $.
    $d f g h t s r D $.  $d x y z w v u f g h t s r F $.  $d f g h t s r R $.
    $d x y z w v u f g h t s r S $.
    ecopopr.1 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\ y e. ( S X. S )
          ) /\ E. z E. w E. v E. u ( ( x = <. z , w >. /\ y = <. v , u >. ) /\
          ( z F u ) = ( w F v ) ) ) } $.
    ${
      $d x y z w v u A $.  $d x y z w v u B $.  $d x y z w v u C $.
      $d x y z w v u D $.
      $( This is the first of several theorems about equivalence relations of
         the kind used in construction of fractions and signed reals, involving
         operations on equivalent classes of ordered pairs.  This theorem
         expresses the relation ` R ` (specified by the hypothesis) in terms of
         its operation ` F ` . $)
      ecopoveq $p |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $=
        ( cv co wceq wb wa oveq12 eqeqan12d an42s opbrop ) COZFOZMPZDOZEOZMPZQZ
        GJMPZHIMPZQZABCDEFGHIJKLUDGQZUEJQZUGHQZUHIQZUJUMRUNUOSUPUQSUFUKUIULUDGU
        EJMTUGHUHIMTUAUBNUC $.
        $( [16-Aug-1995] $)
    $}

    ${
      ecopopr.com $e |- ( x F y ) = ( y F x ) $.
      $( Assuming the operation ` F ` is commutative, compute the domain the
         relation ` R ` specified by the first hypothesis. $)
      ecopovdm $p |- dom R = ( S X. S ) $=
        ( vf vg cv wcel wa cop wceq co wex cdm cxp copab opabssxp eqsstri ax-mp
        wss dmss dmxpid sseqtri relxp vex opelxp wbr caovcom wb ecopoveq anidms
        mpbiri df-br sylib opex opeldm syl sylbi relssi eqssi ) GUAZHHUBZVHVIVI
        UBZUAZVIGVJUGVHVKUGGANZVIOBNZVIOPVLCNZDNZQRVMENZFNZQRPVNVQISVOVPISRPFTE
        TDTCTZPABUCVJJVRABVIVIUDUEGVJUHUFVIUIUJLMVIVHHHUKLNZMNZQZVIOVSHOVTHOPZW
        AVHOZVSVTHHMULZUMWBWAWAQGOZWCWBWAWAGUNZWEWBWFVSVTISVTVSISRZABVSVTILULWD
        KUOWBWFWGUPABCDEFVSVTVSVTGHIJUQURUSWAWAGUTVAWAWAGVSVTVBVCVDVEVFVG $.
        $( [27-Aug-1995] $)

      ${
        ecopopr.2 $e |- B e. _V $.
        $( Assuming the operation ` F ` is commutative, show that the relation
           ` R ` , specified by the first hypothesis, is symmetric. $)
        ecopovsym $p |- ( A R B -> B R A ) $=
          ( wbr wcel wa cv wceq co vf vg vh vt cxp wb cop opabssxp eqsstri brel
          wex copab eqid breq1 breq2 bibi12d ecopoveq vex caovcom eqeq12i eqcom
          bitri syl6bb ancoms bitr4d 2optocl syl ibi ) GHIOZHGIOZVIGJJUEZPHVKPQ
          VIVJUFZGHVKVKINIARZVKPBRZVKPQVMCRZDRZUGSVNERZFRZUGSQVOVRKTVPVQKTSQFUK
          EUKDUKCUKZQABULVKVKUELVSABVKVKUHUIUJUARZUBRZUGZUCRZUDRZUGZIOZWEWBIOZU
          FGWEIOZWEGIOZUFVLUAUBUCUDGHJJVKVKUMWBGSWFWHWGWIWBGWEIUNWBGWEIUOUPWEHS
          WHVIWIVJWEHGIUOWEHGIUNUPVTJPWAJPQZWCJPWDJPQZQZWFWCWAKTZWDVTKTZSZWGWLW
          FVTWDKTZWAWCKTZSZWOABCDEFVTWAWCWDIJKLUQWRWNWMSWOWPWNWQWMABVTWDKUAURUD
          URMUSABWAWCKUBURUCURMUSUTWNWMVAVBVCWKWJWGWOUFABCDEFWCWDVTWAIJKLUQVDVE
          VFVGVH $.
          $( [27-Aug-1995] $)
      $}

      ${
        $( Assume the operation is closed. $)
        ecopopr.cl $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
        $( Assume the operation is associative. $)
        ecopopr.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
        $( Assume the operation has the cancellation property. $)
        ecopopr.can $e |- ( ( x e. S /\ y e. S ) ->
                   ( ( x F y ) = ( x F z ) -> y = z ) ) $.
        ${
          ecopopr.3 $e |- B e. _V $.
          ecopopr.4 $e |- C e. _V $.
          $( Assuming that operation ` F ` is commutative (second hypothesis),
             closed (third hypothesis), associative (fourth hypothesis), and
             has the cancellation property (fifth hypothesis), show that the
             relation ` R ` , specified by the first hypothesis, is
             transitive. $)
          ecopovtrn $p |- ( ( A R B /\ B R C ) -> A R C ) $=
            ( co vf vg vh vt vs vr cxp wcel w3a wbr wa cv cop wceq wex opabssxp
            copab eqsstri brel simpld anim12i 3anass sylibr eqid anbi1d imbi12d
            wi breq1 breq2 anbi12d imbi1d anbi2d wb ecopoveq 3adant3 oveq12 vex
            3adant1 caov411 caov4 eqtr3i syl6bi caovcl ovex syl2an 3impb 3com12
            3eqtr4g caovcan 3adant3l 3adant1r 3adant2 sylibrd 3optocl mpcom
            syld ) GKKUGZUHZHWQUHZIWQUHZUIZGHJUJZHIJUJZUKZGIJUJZXDWRWSWTUKZUKXA
            XBWRXCXFXBWRWSGHWQWQJRJAULZWQUHBULZWQUHUKXGCULZDULZUMUNXHEULZFULZUM
            UNUKXIXLLTXJXKLTUNUKFUOEUODUOCUOZUKABUQWQWQUGMXMABWQWQUPURZUSUTHIWQ
            WQJSXNUSVAWRWSWTVBVCUAULZUBULZUMZUCULZUDULZUMZJUJZXTUEULZUFULZUMZJU
            JZUKZXQYDJUJZVGGXTJUJZYEUKZGYDJUJZVGXBHYDJUJZUKZYJVGXDXEVGUAUBUCUDU
            EUFGHIKWQKWQVDXQGUNZYFYIYGYJYMYAYHYEXQGXTJVHVEXQGYDJVHVFXTHUNZYIYLY
            JYNYHXBYEYKXTHGJVIXTHYDJVHVJVKYDIUNZYLXDYJXEYOYKXCXBYDIHJVIVLYDIGJV
            IVFXOKUHZXPKUHZUKZXRKUHXSKUHUKZYBKUHZYCKUHZUKZUIZYFXOYCLTZXPYBLTZUN
            ZYGUUCYFXRXSLTZUUDLTZUUGUUELTZUNZUUFUUCYFXOXSLTZXPXRLTZUNZXRYCLTZXS
            YBLTZUNZUKZUUJUUCYAUUMYEUUPYRYSYAUUMVMUUBABCDEFXOXPXRXSJKLMVNVOYSUU
            BYEUUPVMYRABCDEFXRXSYBYCJKLMVNVRVJUUQUUKUUNLTUULUUOLTZUUHUUIUUKUULU
            UNUUOLVPABCXRXSXOYCLUCVQZUDVQZUAVQNPUFVQVSXPXSLTXRYBLTLTUUIUURABCXP
            XSXRYBLUBVQZUUTUUSNPUEVQZVSABCXPXSXRYBLUVAUUTUUSNPUVBVTWAWHWBYPYSUU
            BUUJUUFVGZYQYPYSUUAUVCYTYSYPUUAUVCYSYPUUAUVCYSUUGKUHUUDKUHUVCYPUUAU
            KABXRXSKLOWCABXOYCKLOWCABCUUGUUDUUEKLXPYBLWDQWIWEWFWGWJWKWPYRUUBYGU
            UFVMYSABCDEFXOXPYBYCJKLMVNWLWMWNWO $.
            $( [11-Feb-1996] $)
        $}

        $( Assuming that operation ` F ` is commutative (second hypothesis),
           closed (third hypothesis), associative (fourth hypothesis), and has
           the cancellation property (fifth hypothesis), show that the relation
           ` R ` , specified by the first hypothesis, is an equivalence
           relation. $)
        ecopover $p |- Er R $=
          ( vf vg vh cv vex ecopovsym ecopovtrn iser ) OPQGABCDEFORZPRZGHIJKPSZ
          TABCDEFUCUDQRGHIJKLMNUEQSUAUB $.
          $( [16-Feb-1996] $)
      $}

    $}
  $}

  ${
    $d x y F $.  $d x y S $.  $d x y A $.  $d x y B $.  $d x y C $.
    $d x y D $.
    eceqoveq.2 $e |- B e. _V $.
    $( (We don't bother to eliminate this redundant hypothesis.) $)
    eceqoveq.3 $e |- C e. _V $.
    eceqoveq.4 $e |- D e. _V $.
    eceqoveq.5 $e |- Er R $.
    eceqoveq.6 $e |- dom R = ( S X. S ) $.
    eceqoveq.7 $e |- dom F = ( S X. S ) $.
    eceqoveq.8 $e |- -. (/) e. S $.
    eceqoveq.9 $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
    eceqoveq.10 $e |- ( ( ( A e. S /\ B e. S ) /\
                     ( C e. S /\ D e. S ) ) ->
          ( <. A , B >. R <. C , D >. <-> ( A F D ) = ( B F C ) ) ) $.
    $( Equality of equivalence relation in terms of an operation. $)
    eceqoveq $p |- ( ( A e. S /\ C e. S ) ->
                      ( [ <. A , B >. ] R = [ <. C , D >. ] R <->
                      ( A F D ) = ( B F C ) ) ) $=
      ( wcel c0 wa cop cec wceq co wb wi w3a wbr cvv cxp wer a1i opelxpi adantr
      cdm opex erth bitr3d exp43 3imp wne eleq2i ecdmn0 opelxp 3bitr3i necon1bi
      wn simprbi eqeq1 biimprcd necon3d syl syl5bir com12 3adant3 eleq1 syl5ibr
      caovcl ndmovrcl simprd syl6com necon3bd 3adant1 jcad pm5.1 necon4bid syl6
      pm2.61d com23 imp biimpcd syl5ib simpld 3adant2 3expia eqeq2d simpl con3i
      3exp ndmov simpr eqeq2 3syl pm2.61d1 ) CHSZEHSZUAZDHSZCDUBZGUCZEFUBZGUCZU
      DZCFIUEZDEIUEZUDZUFZXFXGXIXRUGXFXIXGXRXFXIXGXRXFXIXGUHZFHSZXRXFXIXGXTXRUG
      XFXIXGXTXRXFXIUAZXGXTUAZUAZXJXLGUIXNXQYCXJXLGUJHHUKZGULYCMUMGUPZYDUDYCNUM
      YAXJYDSZYBCDHHUNUOXLUJSYCEFUQUMURRUSUTVAXSXTVHZXKXMVBZXOXPVBZUAZXRXSYGYHY
      IXFXIYGYHUGXGYGYAYHYAXKTVBZYGYHXJYESYFYKYAYEYDXJNVCXJGVDCDHHJVEVFZYGXMTUD
      ZYKYHUGXTXMTXMTVBZXGXTXLYESXLYDSYNYBYEYDXLNVCXLGVDEFHHLVEVFZVIVGZYMXKXMXK
      TXNXKTUDZYMXKXMTVJZVKVLVMVNVOVPXIXGYGYIUGXFXIXGUAZXTXOXPXQYSXOHSZXTYSYTXQ
      XPHSZABDEHIQVSXOXPHVQZVRYTXFXTCFHILOPVTWAWBWCWDWEYJXKXMXOXPYHYIWFWGZWHWIW
      TWJWKXHXTXIVHZXRUGZXFXGXTUUEXFXGXTUHZUUDYJXRUUFUUDYHYIXGXTUUDYHUGXFUUDYBY
      HYBYNUUDYHYOUUDYQYNYHUGXIXKTYKXFXIYLVIVGZYQXKXMXMTXNYQYMYRWLVLVMVNVOWDXFX
      TUUDYIUGXGXFXTUAZXIXOXPXQUUHUUAXIUUHYTXQUUAABCFHIQVSUUBWMUUAXIXGDEHIKOPVT
      WNWBWCWOWEUUCWHWPYGUUDXNXQUAXRYGUUDXNXQUUDXNYGYQUUGYGXMTXKYPWQVRUUDYGXQUU
      DYSVHXPTUDZYGXQUGYSXIXIXGWRWSDEHIKOXAYGXQUUIXOTUDZYGUUHVHUUJUUHXTXFXTXBWS
      CFHILOXAVMXPTXOXCVRXDVOWEXNXQWFWHXEWI $.
      $( [15-Feb-1996] $)
  $}

  ${
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d x y z w v u A $.  $d x y z w v u B $.
    th3qlem1.1 $e |- Er R $.
    th3qlem1.2 $e |- dom R = S $.
    $( Compatibility assumption. $)
    th3qlem1.3 $e |- ( ( ( y e. S /\ w e. S ) /\ ( z e. S /\ v e. S ) ) ->
                 ( ( y R w /\ z R v ) -> ( y F z ) R ( w F v ) ) ) $.
    $( Lemma for Exercise 44 version of Theorem 3Q of [Enderton] p. 60.  The
       third hypothesis is the compatibility assumption. $)
    th3qlem1 $p |- ( ( A e. ( S /. R ) /\ B e. ( S /. R ) ) -> E* x
       E. y E. z ( ( A = [ y ] R /\ B = [ z ] R ) /\
       x = [ ( y F z ) ] R ) ) $=
      ( vu wcel wa cv cec wceq wex cqs co wi wal wmo ee4anv an4 wb eleq1 adantr
      bi2anan9 biimpac eqtr2 anim12i an4s adantl cvv wer a1i ovex simprl simpll
      wbr cdm ecelqsdm eqeltrrd erth mpbird simprr simplr syl22anc mp2and erthi
      sylancr syl2anc eqeq12 syl5ibrcom expimpd exlimdvv syl5bir alrimivv eqeq1
      syl5bi anbi2d 2exbidv eqeq2d oveq12 syl anbi12d cbvex2v syl6bb mo4 sylibr
      eceq1 ) FIHUAZOZGWOOZPZFBQZHRZSZGCQZHRZSZPZAQZWSXBJUBZHRZSZPZCTBTZFDQZHRZ
      SZGEQZHRZSZPZNQZXLXOJUBZHRZSZPZETDTZPZXFXSSZUCZNUDAUDXKAUEWRYGANYEXJYCPZE
      TDTZCTBTWRYFXJYCBCDEUFWRYIYFBCWRYHYFDEYHXEXRPZXIYBPZPWRYFXEXIXRYBUGWRYJYK
      YFWRYJPZYFYKXHYASZYLWTWOOZXCWOOZPZWTXMSZXCXPSZPZYMYJWRYPXEWRYPUHXRXAWPYNX
      DWQYOFWTWOUIGXCWOUIUKUJULYJYSWRXAXNXDXQYSXAXNPYQXDXQPYRFWTXMUMGXCXPUMUNUO
      UPYPYSPZXGXTHUQUQHURYTKUSZXGUQOYTWSXBJUTUSXTUQOYTXLXOJUTUSYTWSXLHVCZXBXOH
      VCZXGXTHVCZYTUUBYQYPYQYRVAZYTWSXLHIIUUAHVDISZYTLUSZYTUUFYNWSIOZLYNYOYSVBZ
      IWSHVEVNZYTUUFXMWOOXLIOZLYTWTXMWOUUEUUIVFIXLHVEVNZVGVHYTUUCYRYPYQYRVIZYTX
      BXOHIIUUAUUGYTUUFYOXBIOZLYNYOYSVJZIXBHVEVNZYTUUFXPWOOXOIOZLYTXCXPWOUUMUUO
      VFIXOHVEVNZVGVHYTUUHUUKUUNUUQUUBUUCPUUDUCUUJUULUUPUURMVKVLVMVOXFXHXSYAVPV
      QVRWCVSVSVTWAXKYDANYFXKXEXSXHSZPZCTBTYDYFXJUUTBCYFXIUUSXEXFXSXHWBWDWEUUTY
      CBCDEWSXLSZXBXOSZPZXEXRUUSYBUVAXAXNUVBXDXQUVAWTXMFWSXLHWNWFUVBXCXPGXBXOHW
      NWFUKUVCXHYAXSUVCXGXTSYMWSXLXBXOJWGXGXTHWNWHWFWIWJWKWLWM $.
      $( [9-Jul-2014] $) $( [3-Aug-1995] $)
  $}

  ${
    $d x y z w v u t s f g h R $.  $d x y z w v u t s f g h S $.
    $d x y z w v u t s f A $.  $d x y z w v u t s f B $.  $d x y z w v u t C $.
    $d x y z w v u t D $.  $d x y z w v u t s f g h F $.
    th3q.1 $e |- R e. _V $.
    th3q.2 $e |- Er R $.
    th3q.3 $e |- dom R = ( S X. S ) $.
    $( Compatibility assumption. $)
    th3q.4 $e |- ( ( ( ( w e. S /\ v e. S ) /\ ( u e. S /\ t e. S ) )
       /\ ( ( s e. S /\ f e. S ) /\ ( g e. S /\ h e. S ) ) ) ->
       ( ( <. w , v >. R <. u , t >. /\ <. s , f >. R <. g , h >. ) ->
       ( <. w , v >. F <. s , f >. ) R ( <. u , t >. F <. g , h >. ) ) ) $.
    $( Lemma for Exercise 44 version of Theorem 3Q of [Enderton] p. 60,
       extended to operations on ordered pairs.  The fourth hypothesis is the
       compatibility assumption. $)
    th3qlem2 $p |- ( ( A e. ( ( S X. S ) /. R ) /\ B e. ( ( S X. S ) /. R ) )
           -> E* z E. w E. v E. u E. t (
           ( A = [ <. w , v >. ] R /\ B = [ <. u , t >. ] R ) /\
                  z = [ ( <. w , v >. F <. u , t >. ) ] R ) ) $=
      ( wcel wa vx vy cxp cqs cv cec wceq co wex wmo cop wbr breq1 anbi1d oveq1
      eqid breq1d imbi12d imbi2d breq2 breq2d anbi2d oveq2 expcom 2optocl com12
      wi imp th3qlem1 eceq1 eqeq2d bi2anan9 oveq12 syl anbi12d cla42ev exlimivv
      opex immoi ) FIIUCZHUDZSGWASTFNUEZHUFZUGZGUAUEZHUFZUGZTZAUEZWBWEMUHZHUFZU
      GZTZUAUINUIZAUJFBUEZCUEZUKZHUFZUGZGDUEZEUEZUKZHUFZUGZTZWIWQXBMUHZHUFZUGZT
      ZEUIDUIZCUIBUIZAUJANUAJUBFGHVTMPQWBVTSJUEZVTSTWEVTSUBUEZVTSTZWBXLHULZWEXM
      HULZTZWJXLXMMUHZHULZVGZXNWQXBHULZXPTZWQWEMUHZXBXMMUHZHULZVGZVGXNWBXBHULZX
      PTZWJYDHULZVGZVGXNXTVGBCDEWBXLIIVTVTUPZWQWBUGZYFYJXNYLYBYHYEYIYLYAYGXPWQW
      BXBHUMUNYLYCWJYDHWQWBWEMUOUQURUSXBXLUGZYJXTXNYMYHXQYIXSYMYGXOXPXBXLWBHUTU
      NYMYDXRWJHXBXLXMMUOVAURUSXNWOISWPISTWTISXAISTTZYFYNYAWBXLUKZKUEZLUEZUKZHU
      LZTZWQYOMUHZXBYRMUHZHULZVGZVGYNYAWEYRHULZTZYCUUBHULZVGZVGYNYFVGNJKLWEXMII
      VTYKYOWEUGZUUDUUHYNUUIYTUUFUUCUUGUUIYSUUEYAYOWEYRHUMVBUUIUUAYCUUBHYOWEWQM
      VCUQURUSYRXMUGZUUHYFYNUUJUUFYBUUGYEUUJUUEXPYAYRXMWEHUTVBUUJUUBYDYCHYRXMXB
      MVCVAURUSYNWBISXLISTYPISYQISTTUUDRVDVEVFVEVHVIXKWNAXJWNBCXIWNDEWMXINUAWQX
      BWOWPVRWTXAVRWBWQUGZWEXBUGZTZWHXEWLXHUUKWDWSUULWGXDUUKWCWRFWBWQHVJVKUULWF
      XCGWEXBHVJVKVLUUMWKXGWIUUMWJXFUGWKXGUGWBWQWEXBMVMWJXFHVJVNVKVOVPVQVQVSVN
      $.
      $( [4-Aug-1995] $)

    ${
      th3q.5 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( ( S X. S ) /. R )
           /\ y e. ( ( S X. S ) /. R ) ) /\ E. w E. v E. u E. t ( (
           x = [ <. w , v >. ] R /\ y = [ <. u , t >. ] R ) /\
           z = [ ( <. w , v >. F <. u , t >. ) ] R ) ) } $.
      $( Corollary of Theorem 3Q of [Enderton] p. 60.  (Unnecessary distinct
         variable restrictions were removed by David Abernethy, 4-Jun-2013.) $)
      th3qcor $p |- Fun G $=
        ( wfun cv cxp cqs wcel wa cop cec co wex copab2 wmo wi th3qlem2 moanimv
        wceq mpbir funoprab funeqi ) NUAAUBZIIUCHUDZUEBUBZVAUEUFZUTDUBEUBUGZHUH
        UPVBFUBGUBUGZHUHUPUFCUBVDVEMUIHUHUPUFGUJFUJEUJDUJZUFZABCUKZUAVGABCVGCUL
        VCVFCULUMCDEFGUTVBHIJKLMOPQRSUNVCVFCUOUQURNVHTUSUQ $.
        $( [4-Jun-2013] $)   $( [12-Nov-1995] $)

      $( Theorem 3Q of [Enderton] p. 60, extended to operations on ordered
         pairs.  (Unnecessary distinct variable restrictions were removed by
         David Abernethy, 4-Jun-2013.) $)
      th3q $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                 ( [ <. A , B >. ] R G [ <. C , D >. ] R ) =
                 [ ( <. A , B >. F <. C , D >. ) ] R ) $=
        ( wcel wa cop cec cxp cqs cv wceq co wex opelxpi ecelqsi anim12i pm3.2i
        syl eqid opeq12 eceq1 eqeq2d anbi1d oveq1 anbi12d cla42egv anbi2d oveq2
        wb 2eximdv sylan9 mp2ani cvv wi ecexg ax-mp w3a eqeq1 bi2anan9 th3qlem2
        3impa 4exbidv ovig mp3an3 sylc ) HMUEIMUEUFZJMUEKMUEUFZUFZHIUGZLUHZMMUI
        ZLUJZUEZJKUGZLUHZWMUEZUFWKDUKZEUKZUGZLUHZULZWPFUKZGUKZUGZLUHZULZUFZWJWO
        QUMZLUHZWTXEQUMZLUHZULZUFZGUNFUNZEUNDUNZWKWPRUMXJULZWGWNWHWQWGWJWLUEWNH
        IMMUOWLWJLTUPUSWHWOWLUEWQJKMMUOWLWOLTUPUSUQWIWKWKULZWPWPULZUFZXJXJULZXP
        XRXSWKUTWPUTURXJUTWGXTYAUFZXBXSUFZXJWTWOQUMZLUHZULZUFZEUNDUNWHXPYGYBDEH
        IMMWRHULWSIULUFWTWJULZYGYBVJWRWSHIVAYHYCXTYFYAYHXBXRXSYHXAWKWKWTWJLVBVC
        VDYHYEXJXJYHYDXIULYEXJULWTWJWOQVEYDXILVBUSVCVFUSVGWHYGXODEXNYGFGJKMMXCJ
        ULXDKULUFXEWOULZXNYGVJXCXDJKVAYIXHYCXMYFYIXGXSXBYIXFWPWPXEWOLVBVCVHYIXL
        YEXJYIXKYDULXLYEULXEWOWTQVIXKYDLVBUSVCVFUSVGVKVLVMWNWQXJVNUEZXPXQVOLVNU
        EYJTXIVNLVPVQAUKZXAULZBUKZXFULZUFZCUKZXLULZUFZGUNFUNEUNDUNXPABCWKWPXJVN
        WMWMRYKWKULZYMWPULZYPXJULZVRYRXNDEFGYSYTUUAYRXNVJYSYTUFYOXHUUAYQXMYSYLX
        BYTYNXGYKWKXAVSYMWPXFVSVTYPXJXLVSVTWBWCCDEFGYKYMLMNOPQSTUAUBUCWAUDWDWEW
        F $.
        $( [19-Dec-2013] $) $( [4-Aug-1995] $)
    $}
  $}

  ${
    $d x y z w v u f g h a b c d A $.  $d x y z w v u f g h a b c d B $.
    $d x y z w v u f a b c d C $.  $d x y z w v u f a b c d D $.
    $d x y z t s g h a b c d G $.  $d x y z w v u f H $.  $d x y z J $.
    $d x y z w v u f K $.  $d x y z w v u f L $.  $d x y z t s g h a b c d R $.
    $d x y z w v u t s f g h a b c d S $.  $d x y ph $.  $d z w v u ps $.
    $d z w v u ch $.
    ovec.1 $e |- H e. _V $.
    ovec.2 $e |- K e. _V $.
    ovec.3 $e |- L e. _V $.
    ovec.4 $e |- R e. _V $.
    ovec.5 $e |- Er R $.
    ovec.6 $e |- dom R = ( S X. S ) $.
    $( Equivalence relation. $)
    ovec.7 $e |- R = { <. x , y >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. z E. w E. v E. u ( ( x = <. z , w >. /\
                      y = <. v , u >. ) /\ ph ) ) } $.
    $( Substitutions for equivalence relation. $)
    ovec.8 $e |- ( ( ( z = a /\ w = b ) /\ ( v = c /\ u = d ) ) ->
                     ( ph <-> ps ) ) $.
    ovec.9 $e |- ( ( ( z = g /\ w = h ) /\ ( v = t /\ u = s ) ) ->
                     ( ph <-> ch ) ) $.
    $( Pre-operation. $)
    ovec.10 $e |- G = { <. <. x , y >. , z >. | ( ( x e. ( S X. S ) /\
                      y e. ( S X. S ) ) /\
                      E. w E. v E. u E. f ( ( x = <. w , v >. /\
                      y = <. u , f >. ) /\ z = J ) ) } $.
    $( Substitutions for pre-operation. $)
    ovec.11 $e |- ( ( ( w = a /\ v = b ) /\ ( u = g /\ f = h ) ) -> J = K ) $.
    ovec.12 $e |- ( ( ( w = c /\ v = d ) /\ ( u = t /\ f = s ) ) -> J = L ) $.
    ovec.13 $e |- ( ( ( w = A /\ v = B ) /\ ( u = C /\ f = D ) ) -> J = H ) $.
    $( Main operation. $)
    ovec.14 $e |- F = { <. <. x , y >. , z >. | ( ( x e. Q /\ y e. Q ) /\
           E. a E. b E. c E. d ( (
           x = [ <. a , b >. ] R /\ y = [ <. c , d >. ] R ) /\
           z = [ ( <. a , b >. G <. c , d >. ) ] R ) ) } $.
    $( Quotient class for main operation. $)
    ovec.15 $e |- Q = ( ( S X. S ) /. R ) $.
    $( Lemma to establish compatibility of pre-operation. $)
    ovec.16 $e |- ( ( ( ( a e. S /\ b e. S ) /\ ( c e. S /\ d e. S ) ) /\
                       ( ( g e. S /\ h e. S ) /\ ( t e. S /\ s e. S ) ) ) ->
                   ( ( ps /\ ch ) -> K R L ) ) $.
    $( Express an operation on equivalence classes of ordered pairs in terms of
       equivalence class of operations on ordered pairs.  See set.mm for
       additional comments describing the hypotheses.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 4-Jun-2013.) $)
    ovec $p |- ( ( ( A e. S /\ B e. S ) /\ ( C e. S /\ D e. S ) ) ->
                ( [ <. A , B >. ] R F [ <. C , D >. ] R ) =
                   [ H ] R ) $=
      ( wcel wa cop cec co cv wbr opbrop bi2anan9 wb ov3 breqan12d an4s 3imtr4d
      wceq wex copab2 cxp eleq2i anbi12i anbi1i oprabbii eqtri th3q eceq1 eqtrd
      cqs syl ) KQVHLQVHVIMQVHNQVHVIVIZKLVJZPVKMNVJZPVKUAVLWQWRUBVLZPVKZUCPVKZD
      EFUHUIUJUKKLMNPQTJUGUBUASUOUPUQUHVMZQVHUIVMZQVHVIZUJVMZQVHUKVMZQVHVIZVIZS
      VMZQVHTVMZQVHVIZJVMZQVHUGVMZQVHVIZVIZVIBCVIUEUFPVNZXBXCVJZXEXFVJZPVNZXIXJ
      VJZXLXMVJZPVNZVIXQXTUBVLZXRYAUBVLZPVNZVGXHXSBXOYBCABDEFGHIXBXCXEXFPQUSURV
      OACDEFGHIXIXJXLXMPQUTURVOVPXDXKXGXNYEXPVQXDXKVIXGXNVIYCUEYDUFPDEFGHIXBXCX
      IXJUDUERUBQUMVBVAVRDEFGHIXEXFXLXMUDUFRUBQUNVCVAVRVSVTWAUADVMZOVHZEVMZOVHZ
      VIZYFXQPVKWBYHXRPVKWBVIFVMXQXRUBVLPVKWBVIUKWCUJWCUIWCUHWCZVIZDEFWDYFQQWEP
      WNZVHZYHYMVHZVIZYKVIZDEFWDVEYLYQDEFYJYPYKYGYNYIYOOYMYFVFWFOYMYHVFWFWGWHWI
      WJWKWPWSUCWBWTXAWBDEFGHIKLMNUDUCRUBQULVDVAVRWSUCPWLWOWM $.
      $( [4-Jun-2013] $) $( [6-Aug-1995] $)
  $}

  ${
    $d x y z w A $.  $d z w B $.  $d x y z w F $.  $d x y z w R $.
    $d x y z w S $.  $d z w C $.
    ecovcom.1 $e |- C = ( ( S X. S ) /. R ) $.
    $( D,G for AFB $)
    ecovcom.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. z , w >. ] R ) = [ <. D , G >. ] R ) $.
    $( H,J for BFA $)
    ecovcom.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( x e. S /\ y e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. x , y >. ] R ) = [ <. H , J >. ] R ) $.
    ecovcom.4 $e |- D = H $.
    ecovcom.5 $e |- G = J $.
    $( Lemma used to transfer a commutative law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovcom $p |- ( ( A e. C /\ B e. C ) -> ( A F B ) = ( B F A ) ) $=
      ( wceq cv cop cec co oveq1 oveq2 eqeq12d wa opeq12 eceq1 syl mp2an ancoms
      wcel 3eqtr4a 2ecoptocl ) AUAZBUAZUBIUCZCUAZDUAZUBIUCZKUDZVBUSKUDZTEVBKUDZ
      VBEKUDZTEFKUDZFEKUDZTABCDEFJJIGOUSETVCVEVDVFUSEVBKUEUSEVBKUFUGVBFTVEVGVFV
      HVBFEKUFVBFEKUEUGUQJUNURJUNUHZUTJUNVAJUNUHZUHHLUBZIUCZMNUBZIUCZVCVDHMTZLN
      TZVLVNTZRSVOVPUHVKVMTVQHLMNUIVKVMIUJUKULPVJVIVDVNTQUMUOUP $.
      $( [4-Jun-2013] $) $( [29-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d x y z w v u C $.
    $d x y z w v u F $.  $d x y z w v u R $.  $d x y z w v u S $.
    $d z w v u D $.
    ecovass.1 $e |- D = ( ( S X. S ) /. R ) $.
    ecovass.2 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. z , w >. ] R ) = [ <. G , H >. ] R ) $.
    ecovass.3 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. v , u >. ] R ) = [ <. N , Q >. ] R ) $.
    ecovass.4 $e |- ( ( ( G e. S /\ H e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. G , H >. ] R F [ <. v , u >. ] R ) = [ <. J , K >. ] R ) $.
    ecovass.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( N e. S /\ Q e. S ) ) ->
 ( [ <. x , y >. ] R F [ <. N , Q >. ] R ) = [ <. L , M >. ] R ) $.
    ecovass.6 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( G e. S /\ H e. S ) ) $.
    ecovass.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( N e. S /\ Q e. S ) ) $.
    ecovass.8 $e |- J = L $.
    ecovass.9 $e |- K = M $.
    $( Lemma used to transfer an associative law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovass $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( ( A F B ) F C ) = ( A F ( B F C ) ) ) $=
      ( cv cop cec co wceq oveq1 oveq1d eqeq12d oveq2 oveq2d wcel wa w3a opeq12
      eceq1 syl mp2an adantr sylan eqtrd 3impa adantl sylan2 3eqtr4a 3ecoptocl
      3impb ) AUKZBUKZULLUMZCUKZDUKZULLUMZNUNZEUKZFUKZULLUMZNUNZVSWBWFNUNZNUNZU
      OGWBNUNZWFNUNZGWHNUNZUOGHNUNZWFNUNZGHWFNUNZNUNZUOWMINUNZGHINUNZNUNZUOABCD
      EFGHIMLJUBVSGUOZWGWKWIWLWTWCWJWFNVSGWBNUPUQVSGWHNUPURWBHUOZWKWNWLWPXAWJWM
      WFNWBHGNUSUQXAWHWOGNWBHWFNUPUTURWFIUOZWNWQWPWSWFIWMNUSXBWOWRGNWFIHNUSUTUR
      VQMVAVRMVAVBZVTMVAWAMVAVBZWDMVAWEMVAVBZVCQRULZLUMZSTULZLUMZWGWIQSUOZRTUOZ
      XGXIUOZUIUJXJXKVBXFXHUOXLQRSTVDXFXHLVEVFVGXCXDXEWGXGUOXCXDVBZXEVBWGOPULLU
      MZWFNUNZXGXMWGXOUOXEXMWCXNWFNUCUQVHXMOMVAPMVAVBXEXOXGUOUGUEVIVJVKXCXDXEWI
      XIUOXCXDXEVBZVBWIVSUAKULLUMZNUNZXIXPWIXRUOXCXPWHXQVSNUDUTVLXPXCUAMVAKMVAV
      BXRXIUOUHUFVMVJVPVNVO $.
      $( [4-Jun-2013] $) $( [31-Aug-1995] $)
  $}

  ${
    $d x y z w v u A $.  $d z w v u B $.  $d w v u C $.  $d x y z w v u F $.
    $d x y z w v u R $.  $d x y z w v u S $.  $d x y z w v u G $.
    $d z w v u D $.
    ecovdi.1 $e |- D = ( ( S X. S ) /. R ) $.
    $( M,N for BFC $)
    ecovdi.2 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. z , w >. ] R F [ <. v , u >. ] R ) = [ <. M , N >. ] R ) $.
    $( H,J for AG(BFC) $)
    ecovdi.3 $e |- ( ( ( x e. S /\ y e. S ) /\ ( M e. S /\ N e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. M , N >. ] R ) = [ <. H , J >. ] R ) $.
    $( W,X for AGB $)
    ecovdi.4 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. z , w >. ] R ) = [ <. W , X >. ] R ) $.
    $( Y,Z for AGC $)
    ecovdi.5 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
 ( [ <. x , y >. ] R G [ <. v , u >. ] R ) = [ <. Y , Z >. ] R ) $.
    $( K,L for (AGB)F(AGC) $)
    ecovdi.6 $e |- ( ( ( W e. S /\ X e. S ) /\ ( Y e. S /\ Z e. S ) ) ->
 ( [ <. W , X >. ] R F [ <. Y , Z >. ] R ) = [ <. K , L >. ] R ) $.
    ecovdi.7 $e |- ( ( ( z e. S /\ w e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( M e. S /\ N e. S ) ) $.
    ecovdi.8 $e |- ( ( ( x e. S /\ y e. S ) /\ ( z e. S /\ w e. S ) ) ->
                     ( W e. S /\ X e. S ) ) $.
    ecovdi.9 $e |- ( ( ( x e. S /\ y e. S ) /\ ( v e. S /\ u e. S ) ) ->
                     ( Y e. S /\ Z e. S ) ) $.
    ecovdi.10 $e |- H = K $.
    ecovdi.11 $e |- J = L $.
    $( Lemma used to transfer a distributive law via an equivalence relation.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 4-Jun-2013.) $)
    ecovdi $p |- ( ( A e. D /\ B e. D /\ C e. D ) ->
                 ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) ) $=
      ( cv cop co wceq oveq1 oveq12d eqeq12d oveq2d oveq2 oveq1d wcel wa opeq12
      cec w3a eceq1 syl mp2an adantl sylan2 eqtrd 3impb oveqan12d syl2an 3impdi
      3eqtr4a 3ecoptocl ) AUPZBUPZUQKVIZCUPZDUPZUQKVIZEUPZFUPZUQKVIZMURZNURZWEW
      HNURZWEWKNURZMURZUSGWLNURZGWHNURZGWKNURZMURZUSGHWKMURZNURZGHNURZWSMURZUSG
      HIMURZNURZXCGINURZMURZUSABCDEFGHILKJUEWEGUSZWMWQWPWTWEGWLNUTXIWNWRWOWSMWE
      GWHNUTWEGWKNUTVAVBWHHUSZWQXBWTXDXJWLXAGNWHHWKMUTVCXJWRXCWSMWHHGNVDVEVBWKI
      USZXBXFXDXHXKXAXEGNWKIHMVDVCXKWSXGXCMWKIGNVDVCVBWCLVFWDLVFVGZWFLVFWGLVFVG
      ZWILVFWJLVFVGZVJOPUQZKVIZQRUQZKVIZWMWPOQUSZPRUSZXPXRUSZUNUOXSXTVGXOXQUSYA
      OPQRVHXOXQKVKVLVMXLXMXNWMXPUSXLXMXNVGZVGWMWESTUQKVIZNURZXPYBWMYDUSXLYBWLY
      CWENUFVCVNYBXLSLVFTLVFVGYDXPUSUKUGVOVPVQXLXMXNWPXRUSXLXMVGZXLXNVGZVGWPUAU
      BUQKVIZUCUDUQKVIZMURZXRYEYFWNYGWOYHMUHUIVRYEUALVFUBLVFVGUCLVFUDLVFVGYIXRU
      SYFULUMUJVSVPVTWAWB $.
      $( [4-Jun-2013] $) $( [2-Sep-1995] $)
  $}

