$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Operations
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Extend class notation to include the value of an operation ` F ` (such as
     ` + ` ) for two arguments ` A ` and ` B ` .  Note that the syntax is
     simply three class symbols in a row surrounded by parentheses.  Since
     operation values are the only possible class expressions consisting of
     three class expressions in a row surrounded by parentheses, the syntax is
     unambiguous.  (For an example of how syntax could become ambiguous if we
     are not careful, see the comment in ~ cneg .) $)
  co $a class ( A F B ) $.

  $( Extend class notation to include class abstraction (class builder) of
     nested ordered pairs. $)
  copab2 $a class { <. <. x , y >. , z >. | ph } $.

  $( Define the value of an operation.  Definition of operation value in
     [Enderton] p. 79.  Note that the syntax is simply three class expressions
     in a row bracketed by parentheses.  There are no restrictions of any kind
     on what those class expressions may be, although only certain kinds of
     class expressions - a binary operation ` F ` and its arguments ` A ` and
     ` B ` - will be useful for proving meaningful theorems.  For example, if
     class ` F ` is the operation ` + ` and arguments ` A ` and ` B ` are ` 3 `
     and ` 2 ` , the expression ` ( 3 + 2 ) ` can be proved to equal ` 5 ` (see
     ~ 3p2e5 ).  This definition is well-defined, although not very meaningful,
     when classes ` A ` and/or ` B ` are proper classes (i.e. are not sets);
     see ~ ovprc1 and ~ ovprc2 .  On the other hand, we often find uses for
     this definition when ` F ` is a proper class, such as ` +o ` in ~ oav .
     ` F ` is normally equal to a class of nested ordered pairs of the form
     defined by ~ df-oprab . $)
  df-ov $a |- ( A F B ) = ( F ` <. A , B >. ) $.

  ${
    $d x w $.  $d y w $.  $d z w $.  $d w ph $.
    $( Define the class abstraction (class builder) of a collection of nested
       ordered pairs (for use in defining operations).  This is a special case
       of Definition 4.16 of [TakeutiZaring] p. 14.  Normally ` x ` , ` y ` ,
       and ` z ` are distinct, although the definition doesn't strictly require
       it.  See ~ df-ov for the value of an operation.  The brace notation is
       called "class abstraction" by Quine; it is also called a "class builder"
       in the literature.  The value of the most common operation class builder
       is given by ~ ov2 . $)
    df-oprab $a |- { <. <. x , y >. , z >. | ph } =
                  { w | E. x E. y E. z ( w = <. <. x , y >. , z >. /\ ph ) } $.
  $}

  $( Equality theorem for operation value. $)
  oveq $p |- ( F = G -> ( A F B ) = ( A G B ) ) $=
    ( wceq cop cfv co fveq1 df-ov 3eqtr4g ) CDEABFZCGLDGABCHABDHLCDIABCJABDJK
    $.
    $( [28-Feb-1995] $)

  $( Equality theorem for operation value. $)
  oveq1 $p |- ( A = B -> ( A F C ) = ( B F C ) ) $=
    ( wceq cop cfv co opeq1 fveq2d df-ov 3eqtr4g ) ABEZACFZDGBCFZDGACDHBCDHMNOD
    ABCIJACDKBCDKL $.
    $( [28-Feb-1995] $)

  $( Equality theorem for operation value. $)
  oveq2 $p |- ( A = B -> ( C F A ) = ( C F B ) ) $=
    ( wceq cop cfv co opeq2 fveq2d df-ov 3eqtr4g ) ABEZCAFZDGCBFZDGCADHCBDHMNOD
    ABCIJCADKCBDKL $.
    $( [28-Feb-1995] $)

  $( Equality theorem for operation value. $)
  oveq12 $p |- ( ( A = B /\ C = D ) -> ( A F C ) = ( B F D ) ) $=
    ( wceq co oveq1 oveq2 sylan9eq ) ABFCDFACEGBCEGBDEGABCEHCDBEIJ $.
    $( [16-Jul-1995] $)

  ${
    oveq1i.1 $e |- A = B $.
    $( Equality inference for operation value. $)
    oveq1i $p |- ( A F C ) = ( B F C ) $=
      ( wceq co oveq1 ax-mp ) ABFACDGBCDGFEABCDHI $.
      $( [28-Feb-1995] $)

    $( Equality inference for operation value. $)
    oveq2i $p |- ( C F A ) = ( C F B ) $=
      ( wceq co oveq2 ax-mp ) ABFCADGCBDGFEABCDHI $.
      $( [28-Feb-1995] $)

    ${
      oveq12i.2 $e |- C = D $.
      $( Equality inference for operation value.  (The proof was shortened by
         Andrew Salmon, 22-Oct-2011.) $)
      oveq12i $p |- ( A F C ) = ( B F D ) $=
        ( wceq co oveq12 mp2an ) ABHCDHACEIBDEIHFGABCDEJK $.
        $( [22-Oct-2011] $) $( [28-Feb-1995] $)
    $}

    $( Equality inference for operation value. $)
    oveqi $p |- ( C A D ) = ( C B D ) $=
      ( wceq co oveq ax-mp ) ABFCDAGCDBGFECDABHI $.
      $( [24-Nov-2007] $)
  $}

  ${
    oveq1d.1 $e |- ( ph -> A = B ) $.
    $( Equality deduction for operation value. $)
    oveq1d $p |- ( ph -> ( A F C ) = ( B F C ) ) $=
      ( wceq co oveq1 syl ) ABCGBDEHCDEHGFBCDEIJ $.
      $( [13-Mar-1995] $)

    $( Equality deduction for operation value. $)
    oveq2d $p |- ( ph -> ( C F A ) = ( C F B ) ) $=
      ( wceq co oveq2 syl ) ABCGDBEHDCEHGFBCDEIJ $.
      $( [13-Mar-1995] $)

    $( Equality deduction for operation value. $)
    oveqd $p |- ( ph -> ( C A D ) = ( C B D ) ) $=
      ( wceq co oveq syl ) ABCGDEBHDECHGFDEBCIJ $.
      $( [9-Sep-2006] $)

    ${
      oveq12d.2 $e |- ( ph -> C = D ) $.
      $( Equality deduction for operation value.  (The proof was shortened by
         Andrew Salmon, 22-Oct-2011.) $)
      oveq12d $p |- ( ph -> ( A F C ) = ( B F D ) ) $=
        ( wceq co oveq12 syl2anc ) ABCIDEIBDFJCEFJIGHBCDEFKL $.
        $( [22-Oct-2011] $) $( [13-Mar-1995] $)
    $}

    ${
      opreqan12i.2 $e |- ( ps -> C = D ) $.
      $( Equality deduction for operation value. $)
      oveqan12d $p |- ( ( ph /\ ps ) -> ( A F C ) = ( B F D ) ) $=
        ( wceq co oveq12 syl2an ) ACDJEFJCEGKDFGKJBHICDEFGLM $.
        $( [10-Aug-1995] $)

      $( Equality deduction for operation value. $)
      oveqan12rd $p |- ( ( ps /\ ph ) -> ( A F C ) = ( B F D ) ) $=
        ( co wceq oveqan12d ancoms ) ABCEGJDFGJKABCDEFGHILM $.
        $( [10-Aug-1995] $)
    $}
  $}

  ${
    oveq123d.1 $e |- ( ph -> F = G ) $.
    oveq123d.2 $e |- ( ph -> A = B ) $.
    oveq123d.3 $e |- ( ph -> C = D ) $.
    $( Equality deduction for operation value.  (Contributed by FL,
       22-Dec-2008.) $)
    oveq123d $p |- ( ph -> ( A F C ) = ( B G D ) ) $=
      ( co oveqd oveq1d oveq2d 3eqtrd ) ABDFKBDGKCDGKCEGKAFGBDHLABCDGIMADECGJNO
      $.
      $( [22-Dec-2008] $)
  $}

  ${
    $d y F $.  $d y A $.  $d y B $.  $d x y $.
    hbov.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbov.2 $e |- ( y e. F -> A. x y e. F ) $.
    hbov.3 $e |- ( y e. B -> A. x y e. B ) $.
    $( Bound-variable hypothesis builder for operation value. $)
    hbov $p |- ( y e. ( A F B ) -> A. x y e. ( A F B ) ) $=
      ( co cop cfv df-ov hbop hbfv hbxfreq ) ABCDEICDJZEKCDELABPEGABCDFHMNO $.
      $( [4-May-2004] $)
  $}

  ${
    $d y z A $.  $d y z B $.  $d y z F $.  $d x y z $.  $d y ph $.
    hbovd.1 $e |- ( ph -> A. x ph ) $.
    hbovd.2 $e |- ( ph -> ( y e. A -> A. x y e. A ) ) $.
    hbovd.3 $e |- ( ph -> ( y e. F -> A. x y e. F ) ) $.
    hbovd.4 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    $( Deduction version of bound-variable hypothesis builder ~ hbov .  (The
       proof was shortened by Andrew Salmon, 22-Oct-2011.) $)
    hbovd $p |- ( ph -> ( y e. ( A F B ) -> A. x y e. ( A F B ) ) ) $=
      ( cv cop cfv wcel wal co hbopd hbfvd df-ov eleq2i albii 3imtr4g ) ACKZDEL
      ZFMZNZUFBOUCDEFPZNZUHBOABCUDFGIABCDEGHJQRUGUEUCDEFSTZUHUFBUIUAUB $.
      $( [22-Oct-2011] $) $( [13-Dec-2005] $)
  $}

  ${
    $d a ph r s t w $.  $d a r s t w x $.  $d a r s t w y $.  $d a r s t w z $.
    $( The law of concretion.  Special case of Theorem 9.5 of [Quine] p. 61.
       (Contributed by Mario Carneiro, 20-Mar-2013.) $)
    oprabid $p |- ( <. <. x , y >. , z >. e.
        { <. <. x , y >. , z >. | ph } <-> ph ) $=
      ( vw va vt vr vs cv cop wceq wa wex wi vex weq cvv wal wn copab2 opex w3a
      eqvinop biimpi eqeq1 opth1 syl6bi opeq1 eqeq2d wcel wb otthg2 mp2an bitri
      df-3an anbi1i anass 3bitri 3exbii nd5 equcom albii 3imtr3g exdistrf eximi
      excom 3imtr4i anim2i 3syl sylbi weu euequ1 eupick mpan 3impd syl5bi com12
      syl6 syl5 eqcom syl6bb anbi1d 3exbidv imbi1d mpbiri adantr exlimivv com3l
      imbi12d mpdd mpcom 19.8a ex impbid df-oprab elab2 ) EJZBJZCJZKZDJZKZLZAMZ
      DNZCNZBNZAEXCABCDUAXAXBUBXDXHAWRFJZGJZKZLZXKXCLZMZGNFNZXDXHAOZXDXOFGWRXAX
      BWSWTUBDPZUDUEXNXDXPOZFGXLXRXMXLXDXIXALZXPXLXDXMXSWRXKXCUFXIXJXAXBFPUGUHX
      SXLXDXPXSXIHJZIJZKZLZYBXALZMZINHNXLXROZHIXIWSWTBPZCPZUDYEYFHIYCYFYDYCXLWR
      YBXJKZLZXRYCXKYIWRXIYBXJUIUJYJXRXCYILZYKAMZDNCNBNZAOZOYMBHQZCIQZDGQZAMZDN
      ZMCNZMZBNZYKAYMYOYPYRMZMZDNZCNBNZUUBYLUUDBCDYLYOYPMZYQMZAMUUGYRMUUDYKUUHA
      YKYOYPYQUCZUUHYARUKXJRUKYKUUIULIPGPWSWTXTYAXBXJRRYGYHXQUMUNZYOYPYQUPUOUQU
      UGYQAURYOYPYRURUSUTUUFYOUUCDNZMZCNBNZYOUUKCNZMZBNUUBUUEBNZCNUULBNZCNUUFUU
      MUUPUUQCYOUUCBDBDQBSTHBQZUURDSYOYODSDBHVAHBVBZUURYODUUSVCVDVEVFUUEBCVGUUL
      BCVGVHYOUUKBCBCQBSTUURUURCSYOYOCSCBHVAUUSUURYOCUUSVCVDVEUUOUUABUUNYTYOYPY
      RCDCDQCSTICQZUUTDSYPYPDSDCIVAICVBZUUTYPDUVAVCVDVEVIVFVJVKUUBYKAYKUUIUUBAU
      UJUUBYOYPYQAUUBYOYTYPYQAOZOYOBVLUUBYOYTOBHVMYOYTBVNVOYTYPYSUVBYPCVLYTYPYS
      OCIVMYPYSCVNVOYQDVLYSUVBDGVMYQADVNVOVSVSVPVQVRVTYJXDYKXPYNYJXDYIXCLYKWRYI
      XCUFYIXCWAWBZYJXHYMAYJXEYLBCDYJXDYKAUVCWCWDWEWJWFUHWGWHVKWIWKWGWHWLXDAXHX
      EXFXGXHXEDWMXFCWMXGBWMVJWNWOABCDEWPWQ $.
      $( [20-Mar-2013] $)
  $}

  $( The result of an operation is a set. $)
  ovex $p |- ( A F B ) e. _V $=
    ( co cop cfv cvv df-ov fvex eqeltri ) ABCDABEZCFGABCHKCIJ $.
    $( [13-Mar-1995] $)

  ${
    ovprc1.1 $e |- Rel dom F $.
    $( The value of an operation when the first argument is a proper class.
       Note: this theorem is dependent on our particular definitions of
       operation value, function value, and ordered pair. $)
    ovprc1 $p |- ( -. A e. _V -> ( A F B ) = (/) ) $=
      ( cvv wcel wn co cop cfv c0 df-ov cdm wceq wbr df-br brrelexi con3i ndmfv
      sylbir syl syl5eq ) AEFZGZABCHABIZCJZKABCLUDUECMZFZGUFKNUHUCUHABUGOUCABUG
      PABUGDQTRUECSUAUB $.
      $( [16-Jun-2004] $)
  $}

  $( The value of an operation when the second argument is a proper class.
     Note: this theorem is dependent on our particular definitions of operation
     value, function value, and ordered pair. $)
  ovprc2 $p |- ( -. B e. _V -> ( A F B ) = ( A F A ) ) $=
    ( cvv wcel wn cop cfv co opprc2 fveq2d df-ov 3eqtr4g ) BDEFZABGZCHAAGZCHABC
    IAACINOPCABJKABCLAACLM $.
    $( [16-Jun-2004] $)

  ${
    $d y z A $.  $d y z B $.  $d y z C $.  $d y z D $.  $d y z F $.
    $d x y z $.
    $( Move class substitution in and out of an operation.  (The proof was
       shortened by Andrew Salmon, 22-Oct-2011.) $)
    csbovg $p |- ( A e. D -> [_ A / x ]_ ( B F C ) =
           ( [_ A / x ]_ B [_ A / x ]_ F [_ A / x ]_ C ) ) $=
      ( vy vz wcel co csb cop cfv df-ov cv wceq csbeq1 opeq12d hbcsb1 csbeq1a
      csbeq2i csbfv12g eqeq12d ax-17 hbop csbief vtoclg fveq2d syl6eqr 3eqtrd
      vex ) BEIZABCDFJZKABCDLZFMZKABUNKZABFKZMZABCKZABDKZUQJZABUMUOECDFNUAABUNE
      FUBULURUSUTLZUQMVAULUPVBUQAGOZUNKZAVCCKZAVCDKZLZPUPVBPGBEVCBPZVDUPVGVBAVC
      BUNQVHVEUSVFUTAVCBCQAVCBDQRUCAHVCUNVGGUKZAHVEVFAHVCCVIHOVCIAUDZSAHVCDVIVJ
      SUEAOVCPCVEDVFAVCCTAVCDTRUFUGUHUSUTUQNUIUJ $.
      $( [22-Oct-2011] $) $( [12-Nov-2005] $)
  $}

  ${
    $d y A $.  $d y C $.  $d y D $.  $d x y F $.
    $( Move class substitution in and out of an operation. $)
    csbov12g $p |- ( A e. D ->
                 [_ A / x ]_ ( B F C ) = ( [_ A / x ]_ B F [_ A / x ]_ C ) ) $=
      ( wcel co csb csbovg csbconstgfv oveqd eqtrd ) BEGZABCDFHIABCIZABDIZABFIZ
      HOPFHABCDEFJNQFOPABFEKLM $.
      $( [12-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x y C $.  $d y D $.  $d x y F $.
    $( Move class substitution in and out of an operation. $)
    csbov1g $p |- ( A e. D ->
                  [_ A / x ]_ ( B F C ) = ( [_ A / x ]_ B F C ) ) $=
      ( wcel co csb csbov12g csbconstgfv oveq2d eqtrd ) BEGZABCDFHIABCIZABDIZFH
      ODFHABCDEFJNPDOFABDEKLM $.
      $( [12-Nov-2005] $)
  $}

  ${
    $d y A $.  $d x y B $.  $d y D $.  $d x y F $.
    $( Move class substitution in and out of an operation. $)
    csbov2g $p |- ( A e. D ->
                  [_ A / x ]_ ( B F C ) = ( B F [_ A / x ]_ C ) ) $=
      ( wcel co csb csbov12g csbconstgfv oveq1d eqtrd ) BEGZABCDFHIABCIZABDIZFH
      CPFHABCDEFJNOCPFABCEKLM $.
      $( [12-Nov-2005] $)
  $}

  ${
    $d x A $.  $d x y B $.  $d x y C $.  $d y D $.  $d x y F $.  $d x y S $.
    $( A frequently used special case of ~ rcla42ev for operation values. $)
    rcla4eov $p |- ( ( C e. A /\ D e. B /\ S = ( C F D ) ) ->
                 E. x e. A E. y e. B S = ( x F y ) ) $=
      ( cv co wceq oveq1 eqeq2d oveq2 rcla42ev ) GAIZBIZHJZKGEFHJZKGEQHJZKABEFC
      DPEKRTGPEQHLMQFKTSGQFEHNMO $.
      $( [21-Mar-2007] $)
  $}

  ${
    fnopovb.1 $e |- R e. _V $.
    $( Equivalence of operation value and ordered triple membership, analogous
       to ~ fnopfvb . $)
    fnopovb $p |- ( ( F Fn ( A X. B ) /\ C e. A /\ D e. B ) ->
                   ( ( C F D ) = R <-> <. <. C , D >. , R >. e. F ) ) $=
      ( cxp wfn wcel co wceq cop wb wa opelxpi cfv df-ov eqeq1i fnopfvb syl5bb
      sylan2 3impb ) FABHZIZCAJZDBJZCDFKZELZCDMZEMFJZNZUFUGOUEUJUDJZULCDABPUIUJ
      FQZELUEUMOUKUHUNECDFRSUDUJEFGTUAUBUC $.
      $( [17-Dec-2008] $)
  $}

  ${
    $d x z w v $.  $d y z w v $.  $d w ph v $.
    $( Class abstraction for operations in terms of class abstraction of
       ordered pairs. $)
    dfoprab2 $p |- { <. <. x , y >. , z >. | ph } =
                   { <. w , z >. | E. x E. y ( w = <. x , y >. /\ ph ) } $=
      ( vv cv cop wceq wa wex cab copab2 copab excom exrot4 19.42v opeq1 eqeq2d
      3bitr3i pm5.32ri anbi1i anass an32 exbii opex isseti biantru 3exbii bitri
      3bitr4i 19.42vv 2exbii abbii df-oprab df-opab 3eqtr4i ) FGZBGZCGZHZDGZHZI
      ZAJZDKCKBKZFLUREGZVBHZIZVGVAIZAJZCKBKZJZDKEKZFLABCDMVLEDNVFVNFVIVKJZCKBKZ
      EKDKZVPDKEKVFVNVPDEOVQVOEKZDKCKBKVFVODEBCPVRVEBCDVEVJJZEKVEVJEKZJVRVEVEVJ
      EQVOVSEVIVJJZAJVDVJJZAJVOVSWAWBAVJVIVDVJVHVCURVGVAVBRSUAUBVIVJAUCVDVJAUDT
      UEVTVEEVAUSUTUFUGUHUKUIUJVPVMEDVIVKBCULUMTUNABCDFUOVLEDFUPUQ $.
      $( [12-Mar-1995] $)

    $( An operation class abstraction is a relation. $)
    reloprab $p |- Rel { <. <. x , y >. , z >. | ph } $=
      ( vw cv cop wceq wa wex copab2 dfoprab2 relopabi ) EFBFCFGHAICJBJEDABCDKA
      BCDELM $.
      $( [16-Jun-2004] $)
  $}

$(
  @{
    @d x y z w v @.  @d ph v @.
    dfoprab2f.1 @e |- ( ph -> A. w ph ) @.
    @( Class abstraction for operations in terms of class abstraction of
       ordered pairs.  This is a version of ~ dfoprab2 with bound-variable
       hypothesis instead of distinct variable requirement. @)
    dfoprab2f @p |- { <. <. x , y >. , z >. | ph } =
                   { <. w , z >. | E. x E. y ( w = <. x , y >. /\ ph ) } @=
      ( vv copab2 cv cop wceq wa wex copab dfoprab2 ax-17 hban hbex weq eqeq1
      anbi1d 2exbidv cbvopab1
      eqtri ) ABCDHGIZBICIJZKZALZCMZBMZGDNEIZUFKZALZCMBM
      ZEDNABCDGOUJUNGDEUIEBUHECUGAEUGEPFQRRUNGPGESZUHUMBCUOUGULAUEUKUFTUAUBUCUD
      @.
  @}
$)

  ${
    $d x w v $.  $d y v $.  $d z v $.  $d v ph $.
    $( The abstraction variables in an operation class abstraction are not
       free.  (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.) $)
    hboprab1 $p |- ( w e. { <. <. x , y >. , z >. | ph } ->
                   A. x w e. { <. <. x , y >. , z >. | ph } ) $=
      ( vv copab2 cv cop wceq wa wex cab df-oprab hbe1 hbab hbxfreq ) BEABCDGFH
      BHCHIDHIJAKDLCLZBLZFMABCDFNSBFERBOPQ $.
      $( [24-Jul-2012] $) $( [25-Apr-1995] $)
  $}

  ${
    $d x v $.  $d y w v $.  $d z v $.  $d v ph $.
    $( The abstraction variables in an operation class abstraction are not
       free.  (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 30-Jul-2012.) $)
    hboprab2 $p |- ( w e. { <. <. x , y >. , z >. | ph } ->
                   A. y w e. { <. <. x , y >. , z >. | ph } ) $=
      ( vv copab2 cv cop wceq wa wex cab df-oprab hbe1 hbex hbab hbxfreq ) CEAB
      CDGFHBHCHIDHIJAKDLZCLZBLZFMABCDFNUACFETCBSCOPQR $.
      $( [31-Jul-2012] $)  $( [25-Apr-1995] $)
  $}

  ${
    $d v x $.  $d v y $.  $d v z $.  $d w z $.  $d v ph $.
    $( The abstraction variables in an operation class abstraction are not
       free. $)
    hboprab3 $p |- ( w e. { <. <. x , y >. , z >. | ph } ->
                   A. z w e. { <. <. x , y >. , z >. | ph } ) $=
      ( vv copab2 cv cop wceq wa wex cab df-oprab hbe1 hbex hbab hbxfreq ) DEAB
      CDGFHBHCHIDHIJAKZDLZCLZBLZFMABCDFNUBDFEUADBTDCSDOPPQR $.
      $( [22-Aug-2013] $)
  $}

  ${
    $d u w $.  $d v w x $.  $d v w y $.  $d v w z $.  $d v ph $.
    hboprab.1 $e |- ( ph -> A. w ph ) $.
    $( Bound-variable hypothesis builder for an operation class abstraction. $)
    hboprab $p |- ( u e. { <. <. x , y >. , z >. | ph } ->
                   A. w u e. { <. <. x , y >. , z >. | ph } ) $=
      ( vv copab2 cv cop wceq wa wex cab df-oprab ax-17 hban hbex hbab hbxfreq
      ) EFABCDIHJBJCJKDJKLZAMZDNZCNZBNZHOABCDHPUFEHFUEEBUDECUCEDUBAEUBEQGRSSSTU
      A $.
      $( [22-Aug-2013] $)
  $}

  ${
    $d x z w $.  $d y z w $.  $d w ph $.  $d w ps $.  $d w ch $.
    oprabbid.1 $e |- ( ph -> A. x ph ) $.
    oprabbid.2 $e |- ( ph -> A. y ph ) $.
    oprabbid.3 $e |- ( ph -> A. z ph ) $.
    oprabbid.4 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal operation class abstractions (deduction
       rule). $)
    oprabbid $p |- ( ph ->
           { <. <. x , y >. , z >. | ps } = { <. <. x , y >. , z >. | ch } ) $=
      ( vw cv cop wceq wa wex cab copab2 exbid df-oprab anbi2d abbidv 3eqtr4g )
      AKLDLELMFLMNZBOZFPZEPZDPZKQUDCOZFPZEPZDPZKQBDEFRCDEFRAUHULKAUGUKDGAUFUJEH
      AUEUIFIABCUDJUASSSUBBDEFKTCDEFKTUC $.
      $( [24-Jun-2014] $) $( [21-Feb-2004] $)
  $}

  ${
    $d x z w ph $.  $d y z w ph $.  $d w ps $.  $d w ch $.
    oprabbidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Equivalent wff's yield equal operation class abstractions (deduction
       rule). $)
    oprabbidv $p |- ( ph ->
           { <. <. x , y >. , z >. | ps } = { <. <. x , y >. , z >. | ch } ) $=
      ( ax-17 oprabbid ) ABCDEFADHAEHAFHGI $.
      $( [21-Feb-2004] $)
  $}

  ${
    $d x z w $.  $d y z w $.  $d w ph $.  $d w ps $.
    oprabbii.1 $e |- ( ph <-> ps ) $.
    $( Equivalent wff's yield equal operation class abstractions.  (Unnecessary
       distinct variable restrictions were removed by David Abernethy,
       19-Jun-2012.) $)
    oprabbii $p |- { <. <. x , y >. , z >. | ph }
                 = { <. <. x , y >. , z >. | ps } $=
      ( vw cv wceq copab2 eqid wb a1i oprabbidv ax-mp ) GHZPIZACDEJBCDEJIPKQABC
      DEABLQFMNO $.
      $( [24-Jul-2012] $) $( [28-May-1995] $)
  $}

  ${
    $d x y z $.
    $( Two ways to state the domain of an operation.  (Contributed by FL,
       24-Jan-2010.) $)
    oprab4 $p |-
      { <. <. x , y >. , z >. | ( <. x , y >. e. ( A X. B ) /\ ph ) } =
        { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) } $=
      ( cv cop cxp wcel wa vex opelxp anbi1i oprabbii ) BGZCGZHEFIJZAKPEJQFJKZA
      KBCDRSAPQEFCLMNO $.
      $( [24-Jan-2010] $)
  $}

  ${
    $d x y z w v $.  $d v ph $.  $d v ps $.
    cbvoprab1.1 $e |- ( ph -> A. w ph ) $.
    cbvoprab1.2 $e |- ( ps -> A. x ps ) $.
    cbvoprab1.3 $e |- ( x = w -> ( ph <-> ps ) ) $.
    $( Rule used to change first bound variable in an operation abstraction,
       using implicit substitution.  (The proof was shortened by Andrew Salmon,
       22-Oct-2011.) $)
    cbvoprab1 $p |- { <. <. x , y >. , z >. | ph }
                  = { <. <. w , y >. , z >. | ps } $=
      ( vv cv cop wceq wa wex copab copab2 ax-17 hban hbex opeq1 eqeq2d anbi12d
      exbidv cbvex opabbii dfoprab2 3eqtr4i ) JKZCKZDKZLZMZANZDOZCOZJEPUIFKZUKL
      ZMZBNZDOZFOZJEPACDEQBFDEQUPVBJEUOVACFUNFDUMAFUMFRGSTUTCDUSBCUSCRHSTUJUQMZ
      UNUTDVCUMUSABVCULURUIUJUQUKUAUBIUCUDUEUFACDEJUGBFDEJUGUH $.
      $( [22-Oct-2011] $) $( [20-Dec-2008] $)
  $}

  ${
    $d x y z w v u $.  $d u ph $.  $d u ps $.
    cbvoprab12.1 $e |- ( ph -> A. w ph ) $.
    cbvoprab12.2 $e |- ( ph -> A. v ph ) $.
    cbvoprab12.3 $e |- ( ps -> A. x ps ) $.
    cbvoprab12.4 $e |- ( ps -> A. y ps ) $.
    cbvoprab12.5 $e |- ( ( x = w /\ y = v ) -> ( ph <-> ps ) ) $.
    $( Rule used to change first two bound variables in an operation
       abstraction, using implicit substitution.  (The proof was shortened by
       Andrew Salmon, 22-Oct-2011.) $)
    cbvoprab12 $p |- { <. <. x , y >. , z >. | ph }
                   = { <. <. w , v >. , z >. | ps } $=
      ( vu cv cop wceq wa wex ax-17 hban copab2 opeq12 anbi12d opabbii dfoprab2
      copab eqeq2d cbvex2 3eqtr4i ) MNZCNZDNZOZPZAQZDRCRZMEUFUJFNZGNZOZPZBQZGRF
      RZMEUFACDEUABFGEUAUPVBMEUOVACDFGUNAFUNFSHTUNAGUNGSITUTBCUTCSJTUTBDUTDSKTU
      KUQPULURPQZUNUTABVCUMUSUJUKULUQURUBUGLUCUHUDACDEMUEBFGEMUEUI $.
      $( [22-Oct-2011] $) $( [21-Feb-2004] $)
  $}

  ${
    $d x y z w v u $.  $d u w v ph $.  $d u x y ps $.
    cbvoprab12v.1 $e |- ( ( x = w /\ y = v ) -> ( ph <-> ps ) ) $.
    $( Rule used to change first two bound variables in an operation
       abstraction, using implicit substitution. $)
    cbvoprab12v $p |- { <. <. x , y >. , z >. | ph }
                    = { <. <. w , v >. , z >. | ps } $=
      ( ax-17 cbvoprab12 ) ABCDEFGAFIAGIBCIBDIHJ $.
      $( [8-Oct-2004] $)
  $}

  ${
    $d x z w v $.  $d y z w v $.  $d v ph $.  $d v ps $.
    cbvoprab3.1 $e |- ( ph -> A. w ph ) $.
    cbvoprab3.2 $e |- ( ps -> A. z ps ) $.
    cbvoprab3.3 $e |- ( z = w -> ( ph <-> ps ) ) $.
    $( Rule used to change the third bound variable in an operation
       abstraction, using implicit substitution. $)
    cbvoprab3 $p |- { <. <. x , y >. , z >. | ph } =
                     { <. <. x , y >. , w >. | ps } $=
      ( vv cv wceq wa wex copab copab2 ax-17 hban hbex dfoprab2 anbi2d cbvopab2
      cop 2exbidv 3eqtr4i ) JKCKDKUCLZAMZDNZCNZJEOUFBMZDNZCNZJFOACDEPBCDFPUIULJ
      EFUHFCUGFDUFAFUFFQGRSSUKECUJEDUFBEUFEQHRSSEKFKLZUGUJCDUMABUFIUAUDUBACDEJT
      BCDFJTUE $.
      $( [22-Aug-2013] $)
  $}

  ${
    $d x z w v $.  $d y z w v $.  $d w v ph $.  $d z v ps $.
    cbvoprab3v.1 $e |- ( z = w -> ( ph <-> ps ) ) $.
    $( Rule used to change the third bound variable in an operation
       abstraction, using implicit substitution.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Jun-2012.) $)
    cbvoprab3v $p |- { <. <. x , y >. , z >. | ph } =
                     { <. <. x , y >. , w >. | ps } $=
      ( ax-17 cbvoprab3 ) ABCDEFAFHBEHGI $.
      $( [24-Jul-2012] $) $( [8-Oct-2004] $)
  $}

  ${
    elimdelov.1 $e |- ( ph -> C e. ( A F B ) ) $.
    elimdelov.2 $e |- Z e. ( X F Y ) $.
    $( Eliminate a hypothesis which is a predicate expressing membership in the
       result of an operator (deduction version).  See ~ ghomgrplem for an
       example of its use.  (Contributed by Paul Chapman, 25-Mar-2008.) $)
    elimdelov $p |- if ( ph , C , Z ) e.
                     ( if ( ph , A , X ) F if ( ph , B , Y ) ) $=
      ( cif co wcel iftrue eqeltrd oveq12d eleqtrrd wn iffalse syl6eqel pm2.61i
      ) AADHKZABFKZACGKZELZMAUBBCELZUEAUBDUFADHNIOAUCBUDCEABFNACGNPQARZUBFGELZU
      EUGUBHUHADHSJTUGUCFUDGEABFSACGSPQUA $.
      $( [25-Mar-2008] $)
  $}

  ${
    $d x z w $.  $d y z w $.  $d w ph $.
    $( The domain of an operation class abstraction.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Jun-2012.) $)
    dmoprab $p |- dom { <. <. x , y >. , z >. | ph } =
                  { <. x , y >. | E. z ph } $=
      ( vw copab2 cdm cv cop wceq wa wex copab cab dfoprab2 dmeqi dmopab exrot3
      19.42v 2exbii bitri abbii df-opab eqtr4i 3eqtri ) ABCDFZGEHBHCHIJZAKZCLBL
      ZEDMZGUIDLZENZADLZBCMZUFUJABCDEOPUIEDQULUGUMKZCLBLZENUNUKUPEUKUHDLZCLBLUP
      UHDBCRUQUOBCUGADSTUAUBUMBCEUCUDUE $.
      $( [24-Jul-2012] $) $( [17-Mar-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( The domain of an operation class abstraction. $)
    dmoprabss $p |- dom { <. <. x , y >. , z >. |
           ( ( x e. A /\ y e. B ) /\ ph ) } C_ ( A X. B ) $=
      ( cv wcel wa copab2 cdm wex copab dmoprab 19.42v opabbii opabssxp eqsstri
      cxp ) BGEHCGFHIZAIZBCDJKUADLZBCMZEFSZUABCDNUCTADLZIZBCMUDUBUFBCTADOPUEBCE
      FQRR $.
      $( [24-Aug-1995] $)
  $}

  ${
    $d x z w $.  $d y z w $.  $d w ph $.
    $( The range of an operation class abstraction.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Apr-2013.) $)
    rnoprab $p |- ran { <. <. x , y >. , z >. | ph } =
                  { z | E. x E. y ph } $=
      ( vw copab2 crn cv cop wceq wa wex copab cab dfoprab2 rneqi rnopab exrot3
      19.41v opex isseti mpbiran 2exbii bitri abbii 3eqtri ) ABCDFZGEHBHZCHZIZJ
      ZAKZCLBLZEDMZGUMELZDNACLBLZDNUGUNABCDEOPUMEDQUOUPDUOULELZCLBLUPULEBCRUQAB
      CUQUKELAUKAESEUJUHUITUAUBUCUDUEUF $.
      $( [19-Apr-2013] $)  $( [30-Aug-2004] $)
  $}

  ${
    $d A y $.  $d x y z $.
    $( The range of a restricted operation class abstraction.  (Contributed by
       Scott Fenton, 21-Mar-2012.) $)
    rnoprab2 $p |- ran { <. <. x , y >. , z >. |
                          ( ( x e. A /\ y e. B ) /\ ph ) } =
                    { z | E. x e. A E. y e. B ph } $=
      ( cv wcel wa copab2 crn wex cab wrex rnoprab r2ex abbii eqtr4i ) BGEHCGFH
      IAIZBCDJKSCLBLZDMACFNBENZDMSBCDOUATDABCEFPQR $.
      $( [22-Mar-2012] $) $( [21-Mar-2012] $)
  $}

  ${
    $d x y z $.
    $( The domain of an operation class abstraction is a relation. $)
    reldmoprab $p |- Rel dom { <. <. x , y >. , z >. | ph } $=
      ( wex copab2 cdm dmoprab relopabi ) ADEBCABCDFGABCDHI $.
      $( [17-Mar-1995] $)

    $( Structure of an operation class abstraction. $)
    oprabss $p |- { <. <. x , y >. , z >. | ph } C_ ( ( _V X. _V ) X. _V ) $=
      ( copab2 cdm crn cxp wrel reloprab relssdmrn ax-mp reldmoprab df-rel mpbi
      cvv wss ssv xpss12 mp2an sstri ) ABCDEZUBFZUBGZHZPPHZPHZUBIUBUEQABCDJUBKL
      UCUFQZUDPQUEUGQUCIUHABCDMUCNOUDRUCUFUDPSTUA $.
      $( [28-Nov-2006] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d w ph $.
    $d x y z w ps $.
    eloprabga.1 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    $( The law of concretion for operation class abstraction.  Compare
       ~ elopab .  (Unnecessary distinct variable restrictions were removed by
       David Abernethy, 19-Jun-2012.)  (Revised by Mario Carneiro,
       19-Dec-2013.) $)
    eloprabga $p |- ( ( A e. V /\ B e. W /\ C e. X ) ->
       ( <. <. A , B >. , C >. e. { <. <. x , y >. , z >. | ph } <-> ps ) ) $=
      ( vw wcel cvv cop wb wceq wa wex copab2 elex w3a wi cv eqeq1 eqcom syl6bb
      opex otthg2 3adant1 sylan9bbr anbi1d pm5.32i 3exbidv df-oprab eleq2i abid
      vex bitr2i eleq1 syl5bb adantl 3anim123i eeeanv sylibr biantrurd 19.41vvv
      cab elisset syl6rbbr adantr 3bitr3d expcom vtocle syl3an ) FINFONZGJNGONZ
      HKNHONZFGPZHPZACDEUAZNZBQZFIUBGJUBHKUBVQVRVSUCZWDUDMWAVTHUIWEMUEZWARZWDWE
      WGSZWFCUEZDUEZPEUEZPZRZASZETDTCTZWIFRZWJGRZWKHRZUCZBSZETDTCTZWCBWHWNWTCDE
      WHWNWSASWTWHWMWSAWGWMWLWARZWEWSWGWMWAWLRXBWFWAWLUFWAWLUGUHVRVSXBWSQVQWIWJ
      FGWKHOOCUSDUSEUSUJUKULUMWSABLUNUHUOWGWOWCQWEWOWFWBNZWGWCXCWFWOMVIZNWOWBXD
      WFACDEMUPUQWOMURUTWFWAWBVAVBVCWEXABQWGWEBWSETDTCTZBSXAWEXEBWEWPCTZWQDTZWR
      ETZUCXEVQXFVRXGVSXHCFOVJDGOVJEHOVJVDWPWQWRCDEVEVFVGWSBCDEVHVKVLVMVNVOVP
      $.
      $( [19-Dec-2013] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d w ph $.
    $d x y z w th $.
    eloprabg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    eloprabg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    eloprabg.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    $( The law of concretion for operation class abstraction.  Compare
       ~ elopab .  (Unnecessary distinct variable restrictions were removed by
       David Abernethy, 19-Jun-2012.) $)
    eloprabg $p |- ( ( A e. V /\ B e. W /\ C e. X ) ->
       ( <. <. A , B >. , C >. e. { <. <. x , y >. , z >. | ph } <-> th ) ) $=
      ( cv wceq syl3an9b eloprabga ) ADEFGHIJKLMEQHRABFQIRCGQJRDNOPST $.
      $( [19-Dec-2013] $) $( [14-Sep-1999] $)
  $}

  ${
    $d ph w $.  $d ps w $.  $d x z w $.  $d y z w $.
    ssoprab2i.1 $e |- ( ph -> ps ) $.
    $( Inference of operation class abstraction subclass from implication.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.) $)
    ssoprab2i $p |- { <. <. x , y >. , z >. | ph } C_
                    { <. <. x , y >. , z >. | ps } $=
      ( vw cv cop wceq wex copab copab2 anim2i 2eximi ssopab2i dfoprab2 3sstr4i
      wa ) GHCHDHIJZASZDKCKZGELTBSZDKCKZGELACDEMBCDEMUBUDGEUAUCCDABTFNOPACDEGQB
      CDEGQR $.
      $( [24-Jul-2012] $) $( [11-Nov-1995] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w ph $.
    $( Restriction of an operation class abstraction. $)
    resoprab $p |- ( { <. <. x , y >. , z >. | ph } |` ( A X. B ) ) =
                  { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ ph ) } $=
      ( vw cv cop wceq wex copab cxp cres wcel copab2 resopab 19.42vv dfoprab2
      wa an12 eleq1 vex opelxp syl6bb anbi1d pm5.32i bitri 2exbii opabbii eqtri
      bitr3i reseq1i 3eqtr4i ) GHZBHZCHZIZJZATZCKBKZGDLZEFMZNZUSUPEOUQFOTZATZTZ
      CKBKZGDLZABCDPZVCNVFBCDPVDUOVCOZVATZGDLVIVAGDVCQVLVHGDVLVKUTTZCKBKVHVKUTB
      CRVMVGBCVMUSVKATZTVGVKUSAUAUSVNVFUSVKVEAUSVKURVCOVEUOURVCUBUPUQEFCUCUDUEU
      FUGUHUIULUJUKVJVBVCABCDGSUMVFBCDGSUN $.
      $( [10-Feb-2007] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d D x y z $.
    $( Restriction of an operator abstraction.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    resoprab2 $p |- ( ( C C_ A /\ D C_ B ) -> ( { <. <. x , y >. , z >. |
                  ( ( x e. A /\ y e. B ) /\ ph ) } |` ( C X. D ) ) =
              { <. <. x , y >. , z >. | ( ( x e. C /\ y e. D ) /\ ph ) } ) $=
      ( wss wa cv wcel copab2 cxp wi wb ssel pm4.71 sylib bicomd resoprab anass
      cres an4 bi2anan9 syl5bb anbi1d syl5bbr oprabbidv syl5eq ) GEIZHFIZJZBKZE
      LZCKZFLZJZAJZBCDMGHNUCUNGLZUPHLZJZUSJZBCDMVBAJZBCDMUSBCDGHUAUMVCVDBCDVCVB
      URJZAJUMVDVBURAUBUMVEVBAVEUTUOJZVAUQJZJUMVBUTVAUOUQUDUKVFUTULVGVAUKUTVFUK
      UTUOOUTVFPGEUNQUTUORSTULVAVGULVAUQOVAVGPHFUPQVAUQRSTUEUFUGUHUIUJ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y z w $.  $d w ph $.
    $( "At most one" is a sufficient condition for an operation class
       abstraction to be a function. $)
    funoprabg $p |- ( A. x A. y E* z ph ->
                    Fun { <. <. x , y >. , z >. | ph } ) $=
      ( vw wmo wal cv cop wceq wa wex copab2 wfun mosubopt alrimiv copab funeqi
      dfoprab2 funopab bitr2i sylib ) ADFCGBGZEHZBHCHIJAKCLBLZDFZEGZABCDMZNZUCU
      FEADBCUDOPUIUEEDQZNUGUHUJABCDESRUEEDTUAUB $.
      $( [28-Aug-2007] $)
  $}

  ${
    $d x y z w $.  $d w ph $.
    funoprab.1 $e |- E* z ph $.
    $( "At most one" is a sufficient condition for an operation class
       abstraction to be a function. $)
    funoprab $p |- Fun { <. <. x , y >. , z >. | ph } $=
      ( wmo wal copab2 wfun gen2 funoprabg ax-mp ) ADFZCGBGABCDHIMBCEJABCDKL $.
      $( [17-Mar-1995] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( Functionality and domain of an operation class abstraction. $)
    fnoprabg $p |- ( A. x A. y ( ph -> E! z ps ) ->
  { <. <. x , y >. , z >. | ( ph /\ ps ) } Fn { <. x , y >. | ph } ) $=
      ( weu wi wal wa copab2 wfun cdm copab wceq wfn wmo eumo imim2i wex a4s wb
      moanimv sylibr 2alimi funoprabg syl dmoprab hba1 hba2 simpl exlimiv ancld
      euex 19.42v syl6ibr impbid2 opabbid syl5eq df-fn sylanbrc ) ABEFZGZDHZCHZ
      ABIZCDEJZKZVFLZACDMZNVFVIOVDVEEPZDHCHVGVBVJCDVBABEPZGVJVAVKABEQRABEUBUCUD
      VECDEUEUFVDVHVEESZCDMVIVECDEUGVDVLACDVCCUHVBDCUIVCVLAUAZCVBVMDVBVLAVEAEAB
      UJUKVBAABESZIVLVBAVNVAVNABEUMRULABEUNUOUPTTUQURVFVIUSUT $.
      $( [28-Aug-2007] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    fnoprab.1 $e |- ( ph -> E! z ps ) $.
    $( Functionality and domain of an operation class abstraction. $)
    fnoprab $p |- { <. <. x , y >. , z >. | ( ph /\ ps ) } Fn
                    { <. x , y >. | ph } $=
      ( weu wi wal wa copab2 copab wfn gen2 fnoprabg ax-mp ) ABEGHZDICIABJCDEKA
      CDLMQCDFNABCDEOP $.
      $( [15-May-1995] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w C $.  $d x y z w F $.
    $( An operation maps to a class to which all values belong. $)
    ffnov $p |- ( F : ( A X. B ) --> C <-> ( F Fn ( A X. B ) /\
         A. x e. A A. y e. B ( x F y ) e. C ) ) $=
      ( vw cxp wf wfn cv cfv wcel wral wa co ffnfv cop wceq fveq2 df-ov syl6eqr
      eleq1d ralxp anbi2i bitri ) CDHZEFIFUGJZGKZFLZEMZGUGNZOUHAKZBKZFPZEMZBDNA
      CNZOGUGEFQULUQUHUKUPGABCDUIUMUNRZSZUJUOEUSUJURFLUOUIURFTUMUNFUAUBUCUDUEUF
      $.
      $( [7-Feb-2004] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y F $.  $d x y R $.  $d x y S $.
    fovcl.1 $e |- F : ( R X. S ) --> C $.
    $( Closure law for an operation. $)
    fovcl $p |- ( ( A e. R /\ B e. S ) -> ( A F B ) e. C ) $=
      ( vx vy wcel wa cv co wral cxp wf wfn ffnov wceq eleq1d ax-mp oveq1 oveq2
      simprbi rcla42v mpi ) ADJBEJKHLZILZFMZCJZIENHDNZABFMZCJZDEOZCFPZUKGUOFUNQ
      UKHIDECFRUDUAUJUMAUHFMZCJHIABDEUGASUIUPCUGAUHFUBTUHBSUPULCUHBAFUCTUEUF $.
      $( [19-Apr-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.  $d z D $.  $d x y z F $.
    $d x y z G $.
    $( Equality of two operations is determined by their values. $)
    eqfnov $p |- ( ( F Fn ( A X. B ) /\ G Fn ( C X. D ) ) -> ( F = G <->
( ( A X. B ) = ( C X. D ) /\ A. x e. A A. y e. B ( x F y ) = ( x G y ) ) ) ) $=
      ( vz cxp wfn wa wceq cv cfv wral co eqfnfv fveq2 df-ov cop eqeq12d anbi2i
      eqeq12i syl6bbr ralxp syl6bb ) GCDJZKHEFJZKLGHMUHUIMZINZGOZUKHOZMZIUHPZLU
      JANZBNZGQZUPUQHQZMZBDPACPZLIUHUIGHRUOVAUJUNUTIABCDUKUPUQUAZMZUNVBGOZVBHOZ
      MUTVCULVDUMVEUKVBGSUKVBHSUBURVDUSVEUPUQGTUPUQHTUDUEUFUCUG $.
      $( [1-Sep-2005] $)
  $}

  ${
    $d A x y $.  $d B x y $.  $d F x y $.  $d G x y $.
    $( Two operators with the same domain are equal iff their values at each
       point in the domain are equal.  (Contributed by Jeff Madsen,
       7-Jun-2010.) $)
    eqfnov2 $p |- ( ( F Fn ( A X. B ) /\ G Fn ( A X. B ) ) ->
                  ( F = G <-> A. x e. A A. y e. B ( x F y ) = ( x G y ) ) ) $=
      ( cxp wfn wa wceq cv co wral eqfnov simpr eqidd ancri impbii syl6bb ) ECD
      GZHFTHIEFJTTJZAKZBKZELUBUCFLJBDMACMZIZUDABCDCDEFNUEUDUAUDOUDUAUDTPQRS $.
      $( [7-Jun-2010] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.  $d x y z w F $.
    $( Representation of an operation class abstraction in terms of its
       values. $)
    fnov $p |- ( F Fn ( A X. B ) <-> F = { <. <. x , y >. , z >. |
                    ( ( x e. A /\ y e. B ) /\ z = ( x F y ) ) } ) $=
      ( vw cxp wfn cv wcel cfv wceq wa copab co copab2 dffn5 wex bitri cop elxp
      anbi1i 19.41vv anass syl6eqr eqeq2d anbi2d pm5.32i 2exbii 3bitr2i opabbii
      fveq2 df-ov dfoprab2 eqtr4i eqeq2i ) FDEHZIFGJZURKZCJZUSFLZMZNZGCOZMFAJZD
      KBJZEKNZVAVFVGFPZMZNZABCQZMGCURFRVEVLFVEUSVFVGUAZMZVKNZBSASZGCOVLVDVPGCVD
      VNVHNZBSASZVCNVQVCNZBSASVPUTVRVCABUSDEUBUCVQVCABUDVSVOABVSVNVHVCNZNVOVNVH
      VCUEVNVTVKVNVCVJVHVNVBVIVAVNVBVMFLVIUSVMFUMVFVGFUNUFUGUHUITUJUKULVKABCGUO
      UPUQT $.
      $( [7-Feb-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z F $.
    $( Representation of an operation class abstraction in terms of its
       values. $)
    fov $p |- ( F : ( A X. B ) --> C <-> ( F = { <. <. x , y >. , z >. |
                  ( ( x e. A /\ y e. B ) /\ z = ( x F y ) ) }
                /\ A. x e. A A. y e. B ( x F y ) e. C ) ) $=
      ( cxp wf wfn cv co wcel wral wa wceq copab2 ffnov fnov anbi1i bitri ) DEH
      ZFGIGUBJZAKZBKZGLZFMBENADNZOGUDDMUEEMOCKUFPOABCQPZUGOABDEFGRUCUHUGABCDEGS
      TUA $.
      $( [7-Feb-2004] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d ph x y z $.
    oprabexd.1 $e |- ( ph -> A e. _V ) $.
    oprabexd.2 $e |- ( ph -> B e. _V ) $.
    oprabexd.3 $e |- ( ( ph /\ ( x e. A /\ y e. B ) ) -> E* z ps ) $.
    oprabexd.4 $e |- ( ph -> F = { <. <. x , y >. , z >. |
                                        ( ( x e. A /\ y e. B ) /\ ps ) } ) $.
    $( Existence of an operator abstraction.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    oprabexd $p |- ( ph -> F e. _V ) $=
      ( cv wcel wa copab2 cvv wmo wal syl2anc cdm wi ex moanimv sylibr alrimivv
      wfun funoprabg syl cxp wss dmoprabss xpexg ssexg sylancr funex eqeltrd )
      AHCMFNDMGNOZBOZCDEPZQLAUTUGZUTUAZQNZUTQNAUSERZDSCSVAAVDCDAURBERZUBVDAURVE
      KUCURBEUDUEUFUSCDEUHUIAVBFGUJZUKVFQNZVCBCDEFGULAFQNGQNVGIJFGQQUMTVBVFQUNU
      OQUTUPTUQ $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    oprabex.1 $e |- A e. _V $.
    oprabex.2 $e |- B e. _V $.
    oprabex.3 $e |- ( ( x e. A /\ y e. B ) -> E* z ph ) $.
    oprabex.4 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ ph ) } $.
    $( Existence of an operation class abstraction. $)
    oprabex $p |- F e. _V $=
      ( cv wcel wa copab2 cvv wfun cdm wmo wi mpbir funoprab cxp xpex dmoprabss
      moanimv ssexi funex mp2an eqeltri ) GBLEMCLFMNZANZBCDOZPKUMQUMRZPMUMPMULB
      CDULDSUKADSTJUKADUFUAUBUNEFUCEFHIUDABCDEFUEUGPUMUHUIUJ $.
      $( [19-Oct-2004] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    oprabex2g.1 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Existence of an operation class abstraction (special case). $)
    oprabex2g $p |- ( ( A e. R /\ B e. S ) -> F e. _V ) $=
      ( wcel wa cv wceq copab2 cvv wfun cdm moeq sylancr moani funoprab cxp wss
      dmoprabss xpexg ssexg funex syl5eqel ) DGKEHKLZIAMDKBMEKLZCMFNZLZABCOZPJU
      JUNQUNRZPKZUNPKUMABCULUKCCFSUAUBUJUODEUCZUDUQPKUPULABCDEUEDEGHUFUOUQPUGTP
      UNUHTUI $.
      $( [27-Aug-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d z C $.
    oprabex2.1 $e |- A e. _V $.
    oprabex2.2 $e |- B e. _V $.
    oprabex2.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B )
             /\ z = C ) } $.
    $( Existence of an operation class abstraction (special case). $)
    oprabex2 $p |- F e. _V $=
      ( cvv wcel oprabex2g mp2an ) DKLEKLGKLHIABCDEFKKGJMN $.
      $( [11-Nov-1995] $)
  $}

  ${
    $d x y z w v u f A $.  $d x y z w v u f B $.  $d x y z w v u f C $.
    $d x y z w v u f D $.  $d x y z w v u f H $.  $d x y z R $.
    $d x y z w v u f S $.
    oprabex3.1 $e |- H e. _V $.
    oprabex3.2 $e |- F = { <. <. x , y >. , z >. | ( ( x e. ( H X. H ) /\
                      y e. ( H X. H ) ) /\
                      E. w E. v E. u E. f ( ( x = <. w , v >. /\
                      y = <. u , f >. ) /\ z = R ) ) } $.
    $( Existence of an operation class abstraction (special case). $)
    oprabex3 $p |- F e. _V $=
      ( cv cop wceq wa wex wmo wcel mosubop cxp xpex anass 2exbii 19.42vv bitri
      moeq mobii mpbir a1i oprabex ) AMZDMEMNOZBMZFMHMNOZPCMGOZPZHQFQZEQDQZABCJ
      JUAZUTIJJKKUBZVAUSCRZULUTSUNUTSPVBUMUOUPPZHQFQZPZEQDQZCRVDCDEULUPCFHUNCGU
      GTTUSVFCURVEDEURUMVCPZHQFQVEUQVGFHUMUOUPUCUDUMVCFHUEUFUDUHUIUJLUK $.
      $( [19-Oct-2004] $)
  $}

  ${
    $d A v x y z w $.  $d ph v $.
    oprabrexex2.1 $e |- A e. _V $.
    oprabrexex2.2 $e |- { <. <. x , y >. , z >. | ph } e. _V $.
    $( Existence of an existentially restricted operation abstraction.
       (Contributed by Jeff Madsen, 11-Jun-2010.) $)
    oprabrexex2 $p |- { <. <. x , y >. , z >. | E. w e. A ph } e. _V $=
      ( vv wrex copab2 cv cop wa wex cab cvv df-oprab rexcom4 exbii wceq bitr2i
      r19.42v bitri abbii eqtri eqeltrri abrexex2 eqeltri ) AEFJZBCDKZILBLCLMDL
      MUAZANZDOZCOZBOZEFJZIPZQUKULUJNZDOZCOZBOZIPURUJBCDIRVBUQIUQUOEFJZBOVBUOEB
      FSVCVABVCUNEFJZCOVAUNECFSVDUTCVDUMEFJZDOUTUMEDFSVEUSDULAEFUCTUDTUDTUBUEUF
      UPEIFGABCDKUPIPQABCDIRHUGUHUI $.
      $( [11-Jun-2010] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z S $.
    $d x y z th $.
    ov.1 $e |- C e. _V $.
    ov.2 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ov.3 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ov.4 $e |- ( z = C -> ( ch <-> th ) ) $.
    ov.5 $e |- ( ( x e. R /\ y e. S ) -> E! z ph ) $.
    ov.6 $e |- F =
                  { <. <. x , y >. , z >. | ( ( x e. R /\ y e. S ) /\ ph ) } $.
    $( The value of an operation class abstraction.  (Unnecessary distinct
       variable restrictions were removed by David Abernethy, 19-Jun-2012.) $)
    ov $p |- ( ( A e. R /\ B e. S ) -> ( ( A F B ) = C <-> th ) ) $=
      ( wcel wa co wceq cop cv copab2 cfv df-ov fveq1i eqtri eqeq1i wfn fnoprab
      copab eleq1 anbi1d anbi2d opelopabg ibir fnopfvb sylancr anbi12d eloprabg
      wb cvv mp3an3 bitrd syl5bb bianabs ) HKTZILTZUAZHIMUBZJUCZDVNHIUDZEUEZKTZ
      FUEZLTZUAZAUAZEFGUFZUGZJUCZVLVLDUAZVMWCJVMVOMUGWCHIMUHVOMWBSUIUJUKVLWDVOJ
      UDWBTZWEVLWBVTEFUNZULVOWGTZWDWFVDVTAEFGRUMVLWHVTVJVSUAZVLEFHIKLVPHUCZVQVJ
      VSVPHKUOUPZVRIUCZVSVKVJVRILUOUQZURUSWGVOJWBNUTVAVJVKJVETWFWEVDNWAWIBUAVLC
      UAWEEFGHIJKLVEWJVTWIABWKOVBWLWIVLBCWMPVBGUEJUCCDVLQUQVCVFVGVHVI $.
      $( [24-Jul-2012] $) $( [16-May-1995] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z ps $.
    ovigg.1 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    ovigg.4 $e |- E* z ph $.
    ovigg.5 $e |- F = { <. <. x , y >. , z >. | ph } $.
    $( The value of an operation class abstraction.  Compare ~ ovig .  The
       condition ` ( x e. R /\ y e. S ) ` is been removed.  (Contributed by FL,
       24-Mar-2007.) $)
    ovigg $p |- ( ( A e. V /\ B e. W /\ C e. X ) ->
                         ( ps -> ( A F B ) = C ) ) $=
      ( wcel w3a cop cfv wceq copab2 co eloprabga wi funoprab funopfvg 3ad2ant3
      wfun mpan2 sylbird df-ov fveq1i eqtri eqeq1i syl6ibr ) FJPZGKPZHLPZQZBFGR
      ZACDEUAZSZHTZFGIUBZHTUSBUTHRVAPZVCABCDEFGHJKLMUCURUPVEVCUDZUQURVAUHVFACDE
      NUEUTHLVAUFUIUGUJVDVBHVDUTISVBFGIUKUTIVAOULUMUNUO $.
      $( [19-Dec-2013] $) $( [24-Mar-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.  $d x y z S $.
    $d x y z ps $.
    ovig.1 $e |- ( ( x = A /\ y = B /\ z = C ) -> ( ph <-> ps ) ) $.
    ovig.2 $e |- ( ( x e. R /\ y e. S ) -> E* z ph ) $.
    ovig.3 $e |- F =
                  { <. <. x , y >. , z >. | ( ( x e. R /\ y e. S ) /\ ph ) } $.
    $( The value of an operation class abstraction (weak version).
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.)  (Revised by Mario Carneiro, 19-Dec-2013.) $)
    ovig $p |- ( ( A e. R /\ B e. S /\ C e. D ) ->
                         ( ps -> ( A F B ) = C ) ) $=
      ( wcel w3a wa wceq cv co 3simpa wb eleq1 bi2anan9 3adant3 anbi12d moanimv
      wmo wi mpbir ovigg mpand ) FJPZGKPZHIPZQUNUORZBFGLUAHSUNUOUPUBCTZJPZDTZKP
      ZRZARZUQBRCDEFGHLJKIURFSZUTGSZETHSZQVBUQABVDVEVBUQUCVFVDUSUNVEVAUOURFJUDU
      TGKUDUEUFMUGVCEUIVBAEUIUJNVBAEUHUKOULUM $.
      $( [19-Dec-2013] $) $( [14-Sep-1999] $)
  $}

  ${
    $d t u v w A $.  $d t u v w B $.  $d t u v x y z C $.  $d t u v x y z D $.
    $d w G $.  $d t u v w z R $.  $d t u v w S $.  $d x y w $.
    ov2gf.a $e |- ( w e. A -> A. x w e. A ) $.
    ov2gf.c $e |- ( w e. A -> A. y w e. A ) $.
    ov2gf.cz $e |- ( w e. A -> A. z w e. A ) $.
    ov2gf.d $e |- ( w e. B -> A. y w e. B ) $.
    ov2gf.dz $e |- ( w e. B -> A. z w e. B ) $.
    ov2gf.1 $e |- ( w e. G -> A. x w e. G ) $.
    ov2gf.2 $e |- ( w e. S -> A. y w e. S ) $.
    ov2gf.3 $e |- ( x = A -> R = G ) $.
    ov2gf.4 $e |- ( y = B -> G = S ) $.
    ov2gf.5 $e |- F = { <. <. x , y >. , z >. | ( ( x e. C /\ y e. D )
                      /\ z = R ) } $.
    $( The value of an operation class abstraction.  A version of ~ ov2g using
       bound-variable hypotheses. $)
    ov2gf $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vt vu vv wcel w3a co cv wa csb copab2 eqeq1 anbi2d cbvoprab3v ax-17 cvv
      wceq wal wi vex hbcsb1 hbcsbg ax-mp hbeq hban eleq1 anbi1d csbeq1a eqeq2d
      anbi12d sylan9bb cbvoprab12 3eqtri oveqi eqid simp3 hblem csbhypf csbeq2d
      hbeleq mpan2 sylan9eq 3adant3 eqeq12d wmo moeq a1i ovig mpi syl5eq ) EGUG
      FHUGJMUGUHZEFKUIEFUDUJZGUGZUEUJZHUGZUKZUFUJZBWPAWNIULZULZUSZUKZUDUEUFUMZU
      IZJKXDEFKAUJZGUGZBUJZHUGZUKZCUJZIUSZUKZABCUMXJWSIUSZUKZABUFUMXDUCXMXOABCU
      FXKWSUSXLXNXJXKWSIUNUOUPXOXCABUFUDUEXOUDUQXOUEUQWRXBAWRAUQADDWSXADUJZWSUG
      ZAUQWPURUGZXPXAUGZXSAUTVAUEVBZABDWPWTURXPWPUGZAUQADWNIUDVBXPWNUGAUQVCVDVE
      VFVGWRXBBWRBUQBDDWSXAXQBUQBDWPWTXTYABUQVCVFVGXFWNUSZXOWOXIUKZWSWTUSZUKXHW
      PUSZXCYBXJYCXNYDYBXGWOXIXFWNGVHVIYBIWTWSAWNIVJVKVLYEYCWRYDXBYEXIWQWOXHWPH
      VHUOYEWTXAWSBWPWTVJVKVLVMVNVOVPWMJJUSZXEJUSJVQXBYFUDUEUFEFJMGHXDWNEUSZWPF
      USZWSJUSZUHWSJXAJYGYHYIVRYGYHXAJUSYIYGYHXABWPLULZJYGXRXAYJUSXTYGBWPWTLURB
      UDEBDUDEOVSWBAUDDEILNSUAVTWAWCBUEDFLJQTUBVTWDWEWFXBUFWGWRUFXAWHWIXDVQWJWK
      WL $.
      $( [19-Dec-2013] $) $( [17-Aug-2006] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d z R $.
    $d x y z S $.
    ov2ag.1 $e |- ( ( x = A /\ y = B ) -> R = S ) $.
    ov2ag.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. C /\ y e. D )
                      /\ z = R ) } $.
    $( The value of an operation class abstraction.  Special case.
       (Contributed by Mario Carneiro, 19-Dec-2013.) $)
    ov2ag $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( wcel w3a wceq co eqid cv simp3 3adant3 eqeq12d wmo wa moeq a1i ovig mpi
      ) DFNEGNIKNOIIPZDEJQIPIRCSZHPZUIABCDEIKFGJASZDPZBSZEPZUJIPZOUJIHIUMUOUPTU
      MUOHIPUPLUAUBUKCUCULFNUNGNUDCHUEUFMUGUH $.
      $( [19-Dec-2013] $)
  $}

  ${
    $d w x y z A $.  $d w y z B $.  $d x y z C $.  $d x y z D $.  $d x w G $.
    $d w z R $.  $d w y S $.
    ov2g.1 $e |- ( x = A -> R = G ) $.
    ov2g.2 $e |- ( y = B -> G = S ) $.
    ov2g.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. C /\ y e. D )
                      /\ z = R ) } $.
    $( The value of an operation class abstraction.  Special case.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.) $)
    ov2g $p |- ( ( A e. C /\ B e. D /\ S e. H ) -> ( A F B ) = S ) $=
      ( vw cv wcel ax-17 ov2gf ) ABCPDEFGHIJKLPQZDRZASUBBSUBCSUAERZBSUCCSUAKRAS
      UAIRBSMNOT $.
      $( [19-Dec-2013] $) $( [14-Sep-1999] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x y z C $.  $d x y z D $.  $d x G $.
    $d z R $.  $d y z S $.
    ov2.1 $e |- S e. _V $.
    ov2.2 $e |- ( x = A -> R = G ) $.
    ov2.3 $e |- ( y = B -> G = S ) $.
    ov2.4 $e |- F = { <. <. x , y >. , z >. | ( ( x e. C /\ y e. D )
                      /\ z = R ) } $.
    $( The value of an operation class abstraction.  Special case.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.) $)
    ov2 $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
      ( wcel cvv co wceq ov2g mp3an3 ) DFPEGPIQPDEJRISLABCDEFGHIJKQMNOTUA $.
      $( [24-Jul-2012] $) $( [16-May-1995] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x G $.  $d z R $.  $d y z S $.
    ov5.1 $e |- S e. _V $.
    ov5.2 $e |- ( x = A -> R = G ) $.
    ov5.3 $e |- ( y = B -> G = S ) $.
    ov5.4 $e |- F = { <. <. x , y >. , z >. | z = R } $.
    $( The value of an operation class abstraction.  Special case.
       (Unnecessary distinct variable restrictions were removed by David
       Abernethy, 19-Jun-2012.) $)
    ov5 $p |- ( ( A e. C /\ B e. D ) -> ( A F B ) = S ) $=
      ( wcel cvv wceq elex cv co copab2 vex pm3.2i biantrur oprabbii ov2 syl2an
      wa eqtri ) DFPDQPEQPDEJUAIREGPDFSEGSABCDEQQHIJKLMNJCTHRZABCUBATQPZBTQPZUI
      ZUKUIZABCUBOUKUOABCUNUKULUMAUCBUCUDUEUFUJUGUH $.
      $( [24-Jul-2012] $) $( [19-Jul-2005] $)
  $}

  ${
    $d x y z w v u f A $.  $d x y z w v u f B $.  $d x y z w v u f C $.
    $d x y z w v u f D $.  $d x y z w v u f H $.  $d x y z R $.
    $d x y z w v u f S $.
    ov3.1 $e |- S e. _V $.
    ov3.2 $e |- ( ( ( w = A /\ v = B ) /\ ( u = C /\ f = D ) ) ->
         R = S ) $.
    ov3.3 $e |- F = { <. <. x , y >. , z >. | ( ( x e. ( H X. H ) /\
                      y e. ( H X. H ) ) /\
                      E. w E. v E. u E. f ( ( x = <. w , v >. /\
                      y = <. u , f >. ) /\ z = R ) ) } $.
    $( The value of an operation class abstraction.  Special case. $)
    ov3 $p |- ( ( ( A e. H /\ B e. H ) /\ ( C e. H /\ D e. H ) ) ->
        ( <. A , B >. F <. C , D >. ) = S ) $=
      ( wa wceq wcel cop cxp cv wex opelxpi anim12i eqid eqeq2d copsex4g mpbiri
      co cvv wi w3a eqeq1 bi2anan9 3impa 4exbidv wmo moeq mosubop anass 19.42vv
      wb 2exbii bitri mobii mpbir a1i ovig mp3an3 sylc ) GOUAHOUASZIOUAJOUASZSZ
      GHUBZOOUCZUAZIJUBZVRUAZSVQDUDZEUDZUBZTZVTFUDZMUDZUBZTZSZLKTZSZMUEFUEEUEDU
      EZVQVTNULLTZVNVSVOWAGHOOUFIJOOUFUGVPWMLLTZLUHWKWODEFMGHIJOOWBGTWCHTSWFITW
      GJTSSKLLQUIUJUKVSWALUMUAWMWNUNPAUDZWDTZBUDZWHTZSZCUDZKTZSZMUEFUEZEUEDUEZW
      MABCVQVTLUMVRVRNWPVQTZWRVTTZXALTZUOXCWLDEFMXFXGXHXCWLVEXFXGSWTWJXHXBWKXFW
      QWEXGWSWIWPVQWDUPWRVTWHUPUQXALKUPUQURUSXECUTZWPVRUAWRVRUASXIWQWSXBSZMUEFU
      EZSZEUEDUEZCUTXKCDEWPXBCFMWRCKVAVBVBXEXMCXDXLDEXDWQXJSZMUEFUEXLXCXNFMWQWS
      XBVCVFWQXJFMVDVGVFVHVIVJRVKVLVM $.
      $( [19-Dec-2013] $) $( [28-May-1995] $)
  $}

  ${
    $d x y z w v u f A $.  $d x y z w v u f B $.  $d z w v u f C $.
    $d w v D $.
    ov4g.1 $e |- F =
          { <. <. x , y >. , z >. | ( ( x e. A /\ y e. B ) /\ z = C ) } $.
    $( Value of an operation given by an ordered-pair class abstraction.  (This
       is the operation analog of ~ fvopab2 .) $)
    ov4g $p |- ( ( x e. A /\ y e. B /\ C e. D ) -> ( x F y ) = C ) $=
      ( vw vv vf vu wcel cv cvv wceq wa ax-17 hbab co elex wsbc cab sbab eqcomd
      equcoms copab2 hbs1 hbeleq hban eleq1 bi2anan9 sylan9eq eqeq2d cbvoprab12
      hbsb anbi12d eqtr4i ov2g syl3an3 ) FGNAOZDNZBOZENZFPNVBVDHUAFQFGUBJKCVBVD
      DELOMOFNZBKOZUCZMUDZNZAJOZUCZLUDZFHVIPVMVIQAJVBVKQVIVMAJLVIUEUFUGZVIFQBKV
      DVGQFVIBKMFUEUFUGZHVCVERZCOZFQZRZABCUHVKDNZVGENZRZVQVMQZRZJKCUHIWDVSJKCAB
      WBWCAWBASACVMVLALCVJAJUITUJUKWBWCBWBBSBCVMVLBLCVJAJBVHBMLVFBKUITUQTUJUKVS
      JSVSKSVKVBQZVGVDQZRZWBVPWCVRWEVTVCWFWAVEVKVBDULVGVDEULUMWGVMFVQWEWFVMVIFV
      NVOUNUOURUPUSUTVA $.
      $( [21-Feb-2004] $)

    $( MAY BE REVISED - this proof eliminates sbab with csb* stuff, but
       the proof ends up longer. $)
    $( Value of an operation given by an ordered-pair class abstraction.  (This
       is the operation analog of ~ fvopab2 .) $)
    ov4gALT $p |- ( ( x e. A /\ y e. B /\ C e. D ) -> ( x F y ) = C ) $=
      ( vw vv wcel cv cvv wceq csb csbeq1a eqcomd wa ax-17 co copab2 vex hbcsb1
      elex equcoms hbeleq hban wal hbcsbg ax-mp eleq1 bi2anan9 sylan9eq anbi12d
      wi eqeq2d cbvoprab12 eqtr4i ov2g syl3an3 ) FGLAMZDLZBMZELZFNLVBVDHUAFOFGU
      EJKCVBVDDEAJMZBKMZFPZPZFHVHNVIVHOAJVBVFOVHVIAVFVHQRUFZVHFOBKVDVGOFVHBVGFQ
      RUFZHVCVESZCMZFOZSZABCUBVFDLZVGELZSZVMVIOZSZJKCUBIVTVOJKCABVRVSAVRATACVIA
      CVFVHJUCZVMVFLZATUDUGUHVRVSBVRBTBCVIVFNLVMVILZWCBUIUPWABACVFVHNWBBTBCVGFK
      UCVMVGLBTUDUJUKUGUHVOJTVOKTVFVBOZVGVDOZSZVRVLVSVNWDVPVCWEVQVEVFVBDULVGVDE
      ULUMWFVIFVMWDWEVIVHFVJVKUNUQUOURUSUTVA $.
      $( [21-Feb-2004] $)
  $}

  ${
    $d w x y z A $.  $d w x y z B $.  $d w x y z C $.  $d w z R $.
    $d w x y z S $.
    ov6g.1 $e |- ( <. x , y >. = <. A , B >. -> R = S ) $.
    ov6g.2 $e |- F = { <. <. x , y >. , z >. | ( <. x , y >. e. C
                      /\ z = R ) } $.
    $( The value of an operation class abstraction.  Special case. $)
    ov6g $p |- ( ( ( A e. G /\ B e. H /\ <. A , B >. e. C ) /\ S e. J )
                     -> ( A F B ) = S ) $=
      ( vw wcel wa cv wceq wex cop w3a co cfv df-ov eqid biidd copsex2g 3adant3
      mpbiri adantr wi eqeq1 anbi1d eqeq2d eqcoms pm5.32i syl6bb 2exbidv anbi2d
      wb wmo moeq mosubop a1i copab2 copab dfoprab2 eleq1 bitr3i 2exbii 19.42vv
      an12 bitri opabbii 3eqtri fvopab3ig 3ad2antl3 mpd syl5eq ) DJPZEKPZDEUAZF
      PZUBZHLPZQZDEIUCWCIUDZHDEIUEWGWCARZBRZUAZSZHHSZQZBTATZWHHSZWEWOWFWAWBWOWD
      WAWBQWOWMHUFWMWMABDEJKWIDSWJESQWMUGUHUJUIUKWDWAWFWOWPULWBORZWKSZCRZGSZQZB
      TATZWLWSHSZQZBTATWOOCWCHFLIWQWCSZXAXDABXEXAWLWTQXDXEWRWLWTWQWCWKUMUNWLWTX
      CWTXCVAWKWCWKWCSGHWSMUOUPUQURUSXCXDWNABXCXCWMWLWSHHUMUTUSXBCVBWQFPZWTCABW
      QCGVCVDVEIWKFPZWTQZABCVFWRXHQZBTATZOCVGXFXBQZOCVGNXHABCOVHXJXKOCXJXFXAQZB
      TATXKXIXLABXIWRXFWTQZQXLWRXMXHWRXFXGWTWQWKFVIUNUQWRXFWTVMVJVKXFXAABVLVNVO
      VPVQVRVSVT $.
      $( [13-Nov-2006] $)
  $}

  ${
    $d ph c $.  $d ps x $.  $d ch x y $.  $d th x y z $.  $d ta x y c $.
    $d R x y z c $.  $d S x y z c $.  $d A x y z c $.  $d B x y z c $.
    $d C x y z c $.
    ovg.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    ovg.2 $e |- ( y = B -> ( ps <-> ch ) ) $.
    ovg.3 $e |- ( z = C -> ( ch <-> th ) ) $.
    ovg.4 $e |- ( ( ta /\ ( x e. R /\ y e. S ) ) -> E! z ph ) $.
    ovg.5 $e |- F = { <. <. x , y >. , z >. |
                                            ( ( x e. R /\ y e. S ) /\ ph ) } $.
    $( The value of an operation class abstraction.  (Contributed by Jeff
       Madsen, 10-Jun-2010.) $)
    ovg $p |- ( ( ta /\ ( A e. R /\ B e. S /\ C e. D ) )
                                            -> ( ( A F B ) = C <-> th ) ) $=
      ( vc wcel w3a wa co wceq cop cv copab2 df-ov fveq1i eqtri eqeq1i wb eqeq2
      cfv wi opeq2 eleq1d bibi12d imbi2d copab wfn weu wal ex alrimivv fnoprabg
      syl eleq1 anbi1d anbi2d opelopabg fnopfvb syl2an vtoclg com12 exp32 3imp2
      ibir vex anbi12d eloprabg adantl bitrd syl5bb biidd bianabs 3adant3 ) EIM
      UBZJNUBZKLUBZUCZUDZIJOUEZKUFZWJWKUDZDUDZDWPIJUGZFUHZMUBZGUHZNUBZUDZAUDZFG
      HUIZUPZKUFZWNWRWOXGKWOWSOUPXGIJOUJWSOXFTUKULUMWNXHWSKUGZXFUBZWREWJWKWLXHX
      JUNZEWJWKWLXKUQWLEWQUDZXKXLXGUAUHZUFZWSXMUGZXFUBZUNZUQXLXKUQUAKLXMKUFZXQX
      KXLXRXNXHXPXJXMKXGUOXRXOXIXFXMKWSURUSUTVAEXFXDFGVBZVCZWSXSUBZXQWQEXDAHVDZ
      UQZGVEFVEXTEYCFGEXDYBSVFVGXDAFGHVHVIWQYAXDWJXCUDZWQFGIJMNWTIUFZXAWJXCWTIM
      VJVKZXBJUFZXCWKWJXBJNVJVLZVMVTXSWSXMXFUAWAVNVOVPVQVRVSWMXJWRUNEXEYDBUDWQC
      UDWRFGHIJKMNLYEXDYDABYFPWBYGYDWQBCYHQWBHUHKUFCDWQRVLWCWDWEWFWMWRDUNZEWJWK
      YIWLWQWRDWQWRWGWHWIWDWE $.
      $( [10-Jun-2010] $)
  $}

  $( The value of a restricted operation.  (Contributed by FL, 10-Nov-2006.) $)
  ovres $p |- ( ( A e. C /\ B e. D ) -> ( A ( F |` ( C X. D ) ) B )
    = ( A F B ) ) $=
    ( wcel wa cop cxp cres cfv co wceq opelxpi fvres syl df-ov 3eqtr4g ) ACFBDF
    GZABHZECDIZJZKZTEKZABUBLABELSTUAFUCUDMABCDNTUAEOPABUBQABEQR $.
    $( [3-Nov-2006] $)

  $( The value of a member of the domain of a subclass of an operation. $)
  oprssov $p |- ( ( ( Fun F /\ G Fn ( C X. D ) /\ G C_ F ) /\
        ( A e. C /\ B e. D ) ) -> ( A F B ) = ( A G B ) ) $=
    ( wfun cxp wfn wss w3a wcel wa cres co wceq ovres adantl cdm eqtr3d reseq2d
    fndm 3ad2ant2 funssres 3adant2 oveqd adantr ) EGZFCDHZIZFEJZKZACLBDLMZMABEU
    INZOZABEOZABFOZUMUOUPPULABCDEQRULUOUQPUMULUNFABULEFSZNZUNFUJUHUSUNPUKUJURUI
    EUIFUBUAUCUHUKUSFPUJEFUDUETUFUGT $.
    $( [23-Aug-2007] $)

  $( A operations's value belongs to its codomain. $)
  fovrn $p |- ( ( F : ( R X. S ) --> C /\ A e. R /\ B e. S ) ->
               ( A F B ) e. C ) $=
    ( cxp wf wcel co wa cop opelxpi cfv df-ov ffvelrn syl5eqel sylan2 3impb ) D
    EGZCFHZADIZBEIZABFJZCIZUBUCKUAABLZTIZUEABDEMUAUGKUDUFFNCABFOTCUFFPQRS $.
    $( [27-Aug-2006] $)

  ${
    $d w x y z A $.  $d w x y z B $.  $d w z C $.  $d w x y z F $.
    $( The range of an operation expressed as a collection of the operation's
       values. $)
    fnrnov $p |- ( F Fn ( A X. B ) -> ran F = { z | E. x e. A E. y e. B
                     z = ( x F y ) } ) $=
      ( vw cxp wfn crn cv cfv wceq wrex cab co fnrnfv cop fveq2 df-ov syl6eqr
      eqeq2d rexxp abbii syl6eq ) FDEHZIFJCKZGKZFLZMZGUFNZCOUGAKZBKZFPZMZBENADN
      ZCOGCUFFQUKUPCUJUOGABDEUHULUMRZMZUIUNUGURUIUQFLUNUHUQFSULUMFTUAUBUCUDUE
      $.
      $( [29-Oct-2006] $)

    $( An onto mapping of an operation expressed in terms of operation
       values. $)
    foov $p |- ( F : ( A X. B ) -onto-> C <-> ( F : ( A X. B ) --> C /\
                  A. z e. C E. x e. A E. y e. B z = ( x F y ) ) ) $=
      ( vw cxp wfo wf cv cfv wceq wrex wral wa co dffo3 cop fveq2 df-ov syl6eqr
      eqeq2d rexxp ralbii anbi2i bitri ) DEIZFGJUIFGKZCLZHLZGMZNZHUIOZCFPZQUJUK
      ALZBLZGRZNZBEOADOZCFPZQHCUIFGSUPVBUJUOVACFUNUTHABDEULUQURTZNZUMUSUKVDUMVC
      GMUSULVCGUAUQURGUBUCUDUEUFUGUH $.
      $( [29-Oct-2006] $)
  $}

  $( An operation's value belongs to its range. $)
  fnovrn $p |- ( ( F Fn ( A X. B ) /\ C e. A /\ D e. B ) ->
                   ( C F D ) e. ran F ) $=
    ( cxp wfn wcel co crn cop opelxpi cfv df-ov fnfvelrn syl5eqel sylan2 3impb
    wa ) EABFZGZCAHZDBHZCDEIZEJZHZUBUCSUACDKZTHZUFCDABLUAUHSUDUGEMUECDENTUGEOPQ
    R $.
    $( [10-Feb-2007] $)

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z D $.  $d x y z F $.
    $( A member of an operation's range is a value of the operation.  (Revised
       by Mario Carneiro, 30-Jan-2014.) $)
    ovelrn $p |- ( F Fn ( A X. B ) -> ( C e. ran F <->
                  E. x e. A E. y e. B C = ( x F y ) ) ) $=
      ( vz cxp wfn crn wcel cv co wceq wrex cab fnrnov eleq2d cvv rexlimivw
      ovex eleq1 mpbiri eqeq1 2rexbidv elab3 syl6bb ) FCDHIZEFJZKEGLZALZBLZFMZN
      ZBDOACOZGPZKEUMNZBDOZACOZUHUIUPEABGCDFQRUOUSGEURESKZACUQUTBDUQUTUMSKUKULF
      UAEUMSUBUCTTUJENUNUQABCDUJEUMUDUEUFUG $.
      $( [30-Jan-2014] $) $( [7-Feb-2007] $)

    $( Membership relation for the values of a function whose image is a
       subclass.  (Contributed by Mario Carneiro, 23-Dec-2013.) $)
    funimassov $p |- ( ( Fun F /\ ( A X. B ) C_ dom F ) ->
        ( ( F " ( A X. B ) ) C_ C <-> A. x e. A A. y e. B ( x F y ) e. C ) ) $=
      ( vz wfun cxp cdm wss wa cima cv cfv wcel wral co funimass4 cop syl6eqr
      wceq fveq2 df-ov eleq1d ralxp syl6bb ) FHCDIZFJKLFUHMEKGNZFOZEPZGUHQANZBN
      ZFRZEPZBDQACQGUHEFSUKUOGABCDUIULUMTZUBZUJUNEUQUJUPFOUNUIUPFUCULUMFUDUAUEU
      FUG $.
      $( [23-Dec-2013] $)

    $( Operation value in an image.  (Contributed by Mario Carneiro,
       23-Dec-2013.) $)
    ovelimab $p |- ( ( F Fn A /\ ( B X. C ) C_ A ) ->
        ( D e. ( F " ( B X. C ) ) <-> E. x e. B E. y e. C D = ( x F y ) ) ) $=
      ( vz wfn cxp wss wa cima wcel cv cfv wceq wrex co syl6bb fvelimab syl6eqr
      cop fveq2 df-ov eqeq1d eqcom rexxp ) GCIDEJZCKLFGUIMNHOZGPZFQZHUIRFAOZBOZ
      GSZQZBERADRHCUIFGUAULUPHABDEUJUMUNUCZQZULUOFQUPURUKUOFURUKUQGPUOUJUQGUDUM
      UNGUEUBUFUOFUGTUHT $.
      $( [29-Jan-2014] $) $( [23-Dec-2013] $)
  $}

  ${
    oprvalconst2.1 $e |- C e. _V $.
    $( The value of a constant operation. $)
    ovconst2 $p |- ( ( R e. A /\ S e. B ) ->
                       ( R ( ( A X. B ) X. { C } ) S ) = C ) $=
      ( wcel wa cxp csn co cop cfv df-ov wceq opelxpi fvconst2 syl syl5eq ) DAG
      EBGHZDEABIZCJIZKDELZUBMZCDEUBNTUCUAGUDCODEABPUACUCFQRS $.
      $( [5-Nov-2006] $)
  $}

  ${
    $d x z A $.  $d y z B $.  $d z C $.
    ab2rexex.1 $e |- A e. _V $.
    ab2rexex.2 $e |- B e. _V $.
    $( Existence of a class abstraction of existentially restricted sets.
       Variables ` x ` and ` y ` are normally free-variable parameters in the
       class expression substituted for ` C ` , which can be thought of as
       ` C ( x , y ) ` .  See comments for ~ abrexex . $)
    ab2rexex $p |- { z | E. x e. A E. y e. B z = C } e. _V $=
      ( cv wceq wrex abrexex abrexex2 ) CIFJBEKACDGBCEFHLM $.
      $( [20-Sep-2011] $)
  $}

  ${
    $d x z A $.  $d y z B $.
    ab2rexex2.1 $e |- A e. _V $.
    ab2rexex2.2 $e |- B e. _V $.
    ab2rexex2.3 $e |- { z | ph } e. _V $.
    $( Existence of an existentially restricted class abstraction. ` ph ` is
       normally has free-variable parameters ` x ` , ` y ` , and ` z ` .
       Compare ~ abrexex2 . $)
    ab2rexex2 $p |- { z | E. x e. A E. y e. B ph } e. _V $=
      ( wrex abrexex2 ) ACFJBDEGACDFHIKK $.
      $( [20-Sep-2011] $)
  $}

  ${
    $d x y S $.  $d x y F $.
    oprssdm.1 $e |- -. (/) e. S $.
    oprssdm.2 $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
    $( Domain of closure of an operation. $)
    oprssdm $p |- ( S X. S ) C_ dom F $=
      ( cxp cdm relxp cv cop wcel wa vex opelxp wn cfv c0 wceq ndmfv df-ov nsyl
      co eqeq1i eleq1 mtbiri sylbir syl con4i sylbi relssi ) ABCCGZDHZCCIAJZBJZ
      KZULLUNCLUOCLMZUPUMLZUNUOCCBNOURUQURPUPDQZRSZUQPZUPDTUTUNUODUCZRSZVAVBUSR
      UNUODUAUDVCVBCLZUQVCVDRCLEVBRCUEUFFUBUGUHUIUJUK $.
      $( [24-Aug-1995] $)
  $}

  $( The value of an operation outside its domain. $)
  ndmovg $p |- ( ( dom F = ( R X. S ) /\ B e. C /\ -. ( A e. R /\ B e. S ) )
              -> ( A F B ) = (/) ) $=
    ( cdm cxp wceq wcel wa wn co c0 cop eleq2 opelxpg sylan9bb notbid cfv df-ov
    ndmfv syl5eq syl6bir 3impia ) FGZDEHZIZBCJZADJBEJKZLZABFMZNIZUHUIKZUKABOZUF
    JZLZUMUNUPUJUHUPUOUGJUIUJUFUGUOPABDECQRSUQULUOFTNABFUAUOFUBUCUDUE $.
    $( [28-Mar-2008] $)

  ${
    $d x A $.  $d x B $.  $d x F $.  $d x S $.
    ndmovcl.1 $e |- dom F = ( S X. S ) $.
    ndmovcl.2 $e |- ( ( A e. S /\ x e. S ) -> ( A F x ) e. S ) $.
    ndmovcl.3 $e |- (/) e. S $.
    $( The closure of an operation outside its domain, when the domain includes
       the empty set.  This technical lemma can make the operation more
       convenient to work in some cases.  It is is dependent on our particular
       definitions of operation value, function value, and ordered pair. $)
    ndmovcl $p |- ( A F B ) e. S $=
      ( cvv wcel co wn wa c0 wceq ndmovg syl6eqel wi eleq1d pm2.61d2 cdm mp3an1
      cxp ex cv oveq2 imbi2d expcom vtoclga ovprc2 syl5ibrcom impcom wrel relxp
      imp releqi mpbir ovprc1 pm2.61i ) BIJZBCEKZDJZUTCIJZVBUTVBVCLZBBEKZDJZUTB
      DJZVGMZVFUTVHLZVFUTVIMVENDEUAZDDUCZOZUTVIVENOFBBIDDEPUBHQUDVGVGVFVGBAUEZE
      KZDJZRZVGVFRABDVMBOZVOVFVGVQVNVEDVMBBEUFSUGVGVMDJVOGUHZUIUOTVDVAVEDBCEUJS
      UKVCVGCDJZMZVBVCVTLZVBVCWAMVANDVLVCWAVANOFBCIDDEPUBHQUDVSVGVBVPVGVBRACDVM
      COZVOVBVGWBVNVADVMCBEUFSUGVRUIULTTUTLVANDBCEVJUMVKUMDDUNVJVKFUPUQURHQUS
      $.
      $( [24-Sep-2004] $)
  $}

  ${
    ndmov.1 $e |- B e. _V $.
    ndmov.2 $e |- dom F = ( S X. S ) $.
    $( The value of an operation outside its domain. $)
    ndmov $p |- ( -. ( A e. S /\ B e. S ) -> ( A F B ) = (/) ) $=
      ( cdm cxp wceq cvv wcel wa wn co c0 ndmovg mp3an12 ) DGCCHIBJKACKBCKLMABD
      NOIFEABJCCDPQ $.
      $( [24-Aug-1995] $)

    ${
      ndmovrcl.3 $e |- -. (/) e. S $.
      $( Reverse closure law, when an operation's domain doesn't contain the
         empty set. $)
      ndmovrcl $p |- ( ( A F B ) e. S -> ( A e. S /\ B e. S ) ) $=
        ( wcel wa co wn c0 ndmov eleq1d mtbiri con4i ) ACHBCHIZABDJZCHZQKZSLCHG
        TRLCABCDEFMNOP $.
        $( [3-Feb-1996] $)
    $}

    ${
      ndmov.3 $e |- A e. _V $.
      $( Any operation is commutative outside its domain. $)
      ndmovcom $p |- ( -. ( A e. S /\ B e. S ) -> ( A F B ) = ( B F A ) ) $=
        ( wcel wa wn co c0 ndmov wceq ancom sylnbi eqtr4d ) ACHZBCHZIZJABDKLBAD
        KZABCDEFMTSRIUALNRSOBACDGFMPQ $.
        $( [24-Aug-1995] $)
    $}

    ${
      ndmov.4 $e |- C e. _V $.
      ndmov.5 $e |- -. (/) e. S $.
      $( Any operation is associative outside its domain, if the domain doesn't
         contain the empty set. $)
      ndmovass $p |- ( -. ( A e. S /\ B e. S /\ C e. S ) ->
              ( ( A F B ) F C ) = ( A F ( B F C ) ) ) $=
        ( wcel wn co c0 wa wceq ndmovrcl sylibr con3i ndmov syl w3a anim1i ovex
        df-3an anim2i 3anass eqtr4d ) ADJZBDJZCDJZUAZKZABELZCELZMABCELZELZULUMD
        JZUJNZKUNMOURUKURUHUINZUJNUKUQUSUJABDEFGIPUBUHUIUJUDQRUMCDEHGSTULUHUODJ
        ZNZKUPMOVAUKVAUHUIUJNZNUKUTVBUHBCDEHGIPUEUHUIUJUFQRAUODEBCEUCGSTUG $.
        $( [24-Aug-1995] $)

      ${
        ndmov.6 $e |- dom G = ( S X. S ) $.
        $( Any operation is distributive outside its domain, if the domain
           doesn't contain the empty set. $)
        ndmovdistr $p |- ( -. ( A e. S /\ B e. S /\ C e. S ) ->
            ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) ) $=
          ( wcel wn co c0 wa wceq ndmovrcl sylibr con3i w3a anim2i 3anass ndmov
          ovex syl anim12i anandi bitri eqtr4d ) ADLZBDLZCDLZUAZMZABCENZFNZOABF
          NZACFNZENZUOUKUPDLZPZMUQOQVBUNVBUKULUMPZPZUNVAVCUKBCDEIHJRUBUKULUMUCZ
          STAUPDFBCEUEKUDUFUOURDLZUSDLZPZMUTOQVHUNVHUKULPZUKUMPZPZUNVFVIVGVJABD
          FGKJRACDFIKJRUGUNVDVKVEUKULUMUHUISTURUSDEACFUEHUDUFUJ $.
          $( [24-Aug-1995] $)
      $}
    $}

    ${
      ndmovord.3 $e |- A e. _V $.
      ndmovord.4 $e |- R C_ ( S X. S ) $.
      ndmovord.5 $e |- -. (/) e. S $.
      ndmovord.6 $e |- ( ( A e. S /\ B e. S /\ C e. S ) ->
                     ( A R B <-> ( C F A ) R ( C F B ) ) ) $.
      $( Elimination of redundant antecedents in an ordering law. $)
      ndmovord $p |- ( C e. S -> ( A R B <-> ( C F A ) R ( C F B ) ) ) $=
        ( wcel wa wbr co wb brel ndmovrcl simprd wi 3expia wn ovex syl pm5.21ni
        anim12i a1d pm2.61i ) AEMZBEMZNZCEMZABDOZCAFPZCBFPZDOZQZUAUJUKUMURLUBUL
        UCURUMUNULUQABEEDGJRUQUOEMZUPEMZNULUOUPEEDCBFUDJRUSUJUTUKUSUMUJCAEFIHKS
        TUTUMUKCBEFGHKSTUGUEUFUHUI $.
        $( [7-Mar-1996] $)
    $}
  $}

  ${
    ndmovordi.3 $e |- A e. _V $.
    ndmovordi.2 $e |- dom F = ( S X. S ) $.
    ndmovordi.4 $e |- R C_ ( S X. S ) $.
    ndmovordi.5 $e |- -. (/) e. S $.
    ndmovordi.6 $e |- ( C e. S -> ( A R B <-> ( C F A ) R ( C F B ) ) ) $.
    $( Elimination of redundant antecedent in an ordering law. $)
    ndmovordi $p |- ( ( C F A ) R ( C F B ) -> A R B ) $=
      ( wcel co wbr ovex brel simpld ndmovrcl syl biimprd mpcom ) CELZCAFMZCBFM
      ZDNZABDNZUEUCELZUBUEUGUDELUCUDEEDCBFOIPQUGUBAELCAEFGHJRQSUBUFUEKTUA $.
      $( [25-Jun-1998] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y C $.  $d x y D $.  $d x y E $.  $d x y ph $.
    $d x y F $.
    caovcld.1 $e |- ( ( ph /\ ( x e. C /\ y e. D ) ) -> ( x F y ) e. E ) $.
    $( Convert an operation closure law to class notation.  (Contributed by
       Mario Carneiro, 26-May-2014.) $)
    caovcld $p |- ( ( ph /\ ( A e. C /\ B e. D ) ) -> ( A F B ) e. E ) $=
      ( cv co wcel wral wa ralrimivva wceq oveq1 eleq1d oveq2 rcla42v mpan9 ) A
      BKZCKZILZHMZCGNBFNDFMEGMODEILZHMZAUFBCFGJPUFUHDUDILZHMBCDEFGUCDQUEUIHUCDU
      DIRSUDEQUIUGHUDEDITSUAUB $.
      $( [26-May-2014] $)
  $}

  ${
    $d x y A $.  $d y B $.  $d x y F $.  $d x y ph $.  $d x y S $.
    caovcl.1 $e |- ( ( x e. S /\ y e. S ) -> ( x F y ) e. S ) $.
    $( Convert an operation closure law to class notation. $)
    caovcl $p |- ( ( A e. S /\ B e. S ) -> ( A F B ) e. S ) $=
      ( wtru wcel wa co tru cv adantl caovcld mpan ) HCEIDEIJCDFKEILHABCDEEEFAM
      ZEIBMZEIJQRFKEIHGNOP $.
      $( [26-May-2014] $) $( [4-Aug-1995] $)
  $}

  ${
    $( General laws for commutative, associative, distributive operations. $)
    $d x y z F $.  $d x y z S $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    $d x y z D $.  $d x y z G $.  $d x y z R $.  $d x y z ph $.
    ${
      caovcomg.1 $e |- ( ( ph /\ ( x e. S /\ y e. S ) ) ->
                          ( x F y ) = ( y F x ) ) $.
      $( Convert an operation commutative law to class notation.  (Revised by
         Mario Carneiro, 1-Jun-2013.) $)
      caovcomg $p |- ( ( ph /\ ( A e. S /\ B e. S ) ) ->
                        ( A F B ) = ( B F A ) ) $=
        ( cv co wceq wral wcel wa ralrimivva oveq1 oveq2 eqeq12d rcla42v mpan9
        ) ABIZCIZGJZUBUAGJZKZCFLBFLDFMEFMNDEGJZEDGJZKZAUEBCFFHOUEUHDUBGJZUBDGJZ
        KBCDEFFUADKUCUIUDUJUADUBGPUADUBGQRUBEKUIUFUJUGUBEDGQUBEDGPRST $.
        $( [1-Jun-2013] $)
    $}

    ${
      caovcom.1 $e |- A e. _V $.
      caovcom.2 $e |- B e. _V $.
      caovcom.3 $e |- ( x F y ) = ( y F x ) $.
      $( Convert an operation commutative law to class notation.  (Revised by
         Mario Carneiro, 1-Jun-2013.) $)
      caovcom $p |- ( A F B ) = ( B F A ) $=
        ( cvv wcel wa co wceq pm3.2i cv a1i caovcomg mp2an ) CIJZSDIJZKCDELDCEL
        MFSTFGNSABCDIEAOZBOZELUBUAELMSUAIJUBIJKKHPQR $.
        $( [1-Jun-2013] $) $( [26-Aug-1995] $)
    $}

    ${
      caovassg.1 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
        ( ( x F y ) F z ) = ( x F ( y F z ) ) ) $.
      $( Convert an operation associative law to class notation.  (Revised by
         Mario Carneiro, 1-Jun-2013.) $)
      caovassg $p |- ( ( ph /\ ( A e. S /\ B e. S /\ C e. S ) ) ->
          ( ( A F B ) F C ) = ( A F ( B F C ) ) ) $=
        ( cv co wceq wral wcel oveq1 oveq1d eqeq12d oveq2 oveq2d rcla43v mpan9
        w3a ralrimivvva ) ABKZCKZILZDKZILZUEUFUHILZILZMZDHNCHNBHNEHOFHOGHOUCEFI
        LZGILZEFGILZILZMZAULBCDHHHJUDULUQEUFILZUHILZEUJILZMUMUHILZEFUHILZILZMBC
        DEFGHHHUEEMZUIUSUKUTVDUGURUHIUEEUFIPQUEEUJIPRUFFMZUSVAUTVCVEURUMUHIUFFE
        ISQVEUJVBEIUFFUHIPTRUHGMZVAUNVCUPUHGUMISVFVBUOEIUHGFISTRUAUB $.
        $( [26-May-2014] $) $( [1-Jun-2013] $)
    $}

    ${
      caovass.1 $e |- A e. _V $.
      caovass.2 $e |- B e. _V $.
      caovass.3 $e |- C e. _V $.
      caovass.4 $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
      $( Convert an operation associative law to class notation.  (Revised by
         Mario Carneiro, 1-Jun-2013.) $)
      caovass $p |- ( ( A F B ) F C ) = ( A F ( B F C ) ) $=
        ( cvv wcel co wceq wtru w3a tru cv wa a1i caovassg mpan mp3an ) DLMZELM
        ZFLMZDEGNFGNDEFGNGNOZHIJPUEUFUGQUHRPABCDEFLGASZBSZGNCSZGNUIUJUKGNGNOPUI
        LMUJLMUKLMQTKUAUBUCUD $.
        $( [26-May-2014] $) $( [26-Aug-1995] $)
    $}

    ${
      caovcan.1 $e |- C e. _V $.
      caovcan.2 $e |- ( ( x e. S /\ y e. S ) ->
                   ( ( x F y ) = ( x F z ) -> y = z ) ) $.
      $( Convert an operation cancellation law to class notation. $)
      caovcan $p |- ( ( A e. S /\ B e. S ) ->
                   ( ( A F B ) = ( A F C ) -> B = C ) ) $=
        ( cv co wceq wi oveq1 eqeq12d imbi1d oveq2 imbi12d wcel eqeq1d eqeq1 wa
        eqeq2d eqeq2 imbi2d vtocl vtocl2ga ) AKZBKZHLZUIFHLZMZUJFMZNZDUJHLZDFHL
        ZMZUNNDEHLZUQMZEFMZNABDEGGUIDMZUMURUNVBUKUPULUQUIDUJHOUIDFHOPQUJEMZURUT
        UNVAVCUPUSUQUJEDHRUAUJEFUBSUIGTUJGTUCZUKUICKZHLZMZUJVEMZNZNVDUONCFIVEFM
        ZVIUOVDVJVGUMVHUNVJVFULUKVEFUIHRUDVEFUJUESUFJUGUH $.
        $( [20-Aug-1995] $)
    $}

    ${
      caovord.1 $e |- A e. _V $.
      caovord.2 $e |- B e. _V $.
      caovord.3 $e |- ( z e. S -> ( x R y <-> ( z F x ) R ( z F y ) ) ) $.
      $( Convert an operation ordering law to class notation. $)
      caovord $p |- ( C e. S -> ( A R B <-> ( C F A ) R ( C F B ) ) ) $=
        ( wbr cv co wb wceq oveq1 wi oveq2 breq12d bibi2d wcel wa breq1 bibi12d
        breq1d breq2 breq2d sylan9bb imbi2d vtocl2 vtoclga ) DEGMZCNZDIOZUOEIOZ
        GMZPZUNFDIOZFEIOZGMZPCFHUOFQZURVBUNVCUPUTUQVAGUOFDIRUOFEIRUAUBUOHUCZANZ
        BNZGMZUOVEIOZUOVFIOZGMZPZSVDUSSABDEJKVEDQZVFEQZUDVKUSVDVLVKDVFGMZUPVIGM
        ZPVMUSVLVGVNVJVOVEDVFGUEVLVHUPVIGVEDUOITUGUFVMVNUNVOURVFEDGUHVMVIUQUPGV
        FEUOITUIUFUJUKLULUM $.
        $( [19-Feb-1996] $)

      $( (We don't bother to eliminate this redundant hypothesis.) $)
      caovord2.3 $e |- C e. _V $.
      caovord2.com $e |- ( x F y ) = ( y F x ) $.
      $( Operation ordering law with commuted arguments. $)
      caovord2 $p |- ( C e. S -> ( A R B <-> ( A F C ) R ( B F C ) ) ) $=
        ( wcel wbr co caovord caovcom breq12i syl6bb ) FHODEGPFDIQZFEIQZGPDFIQZ
        EFIQZGPABCDEFGHIJKLRUBUDUCUEGABFDIMJNSABFEIMKNSTUA $.
        $( [27-Feb-1996] $)

      $( (We don't bother to eliminate redundant hypotheses.) $)
      caovord3.4 $e |- D e. _V $.
      $( Ordering law. $)
      caovord3 $p |- ( ( ( B e. S /\ C e. S ) /\
                    ( A F B ) = ( C F D ) ) -> ( A R C <-> D R B ) ) $=
        ( wcel wa co wbr wceq wb caovord2 adantr breq1 sylan9bb ad2antlr bitr4d
        caovord ) EIQZFIQZRZDEJSZFGJSZUAZRDFHTZUNFEJSZHTZGEHTZULUPUMUQHTZUOURUJ
        UPUTUBUKABCDFEHIJKNMLOUCUDUMUNUQHUEUFUKUSURUBUJUOABCGEFHIJPLMUIUGUH $.
        $( [29-Feb-1996] $)
    $}

    ${
      caovdig.1 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
        ( x G ( y F z ) ) = ( ( x G y ) F ( x G z ) ) ) $.
      $( Convert an operation distributive law to class notation.  (Revised by
         Mario Carneiro, 26-Jul-2014.) $)
      caovdig $p |- ( ( ph /\ ( A e. S /\ B e. S /\ C e. S ) ) ->
        ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) ) $=
        ( cv co wceq wral wcel oveq1 eqeq12d oveq2d oveq2 w3a ralrimivvva mpan9
        oveq12d oveq1d rcla43v ) ABLZCLZDLZIMZJMZUGUHJMZUGUIJMZIMZNZDHOCHOBHOEH
        PFHPGHPUAEFGIMZJMZEFJMZEGJMZIMZNZAUOBCDHHHKUBUOVAEUJJMZEUHJMZEUIJMZIMZN
        EFUIIMZJMZURVDIMZNBCDEFGHHHUGENZUKVBUNVEUGEUJJQVIULVCUMVDIUGEUHJQUGEUIJ
        QUDRUHFNZVBVGVEVHVJUJVFEJUHFUIIQSVJVCURVDIUHFEJTUERUIGNZVGUQVHUTVKVFUPE
        JUIGFITSVKVDUSURIUIGEJTSRUFUC $.
        $( [28-Jun-2013] $) $( [25-Aug-1995] $)
    $}

    ${
      caovdirg.1 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
        ( ( x F y ) G z ) = ( ( x G z ) F ( y G z ) ) ) $.
      $( Convert an operation reverse distributive law to class notation. $)
      caovdirg $p |- ( ( ph /\ ( A e. S /\ B e. S /\ C e. S ) ) ->
        ( ( A F B ) G C ) = ( ( A G C ) F ( B G C ) ) ) $=
        ( cv co wceq wral wcel oveq1 oveq1d eqeq12d oveq2 w3a ralrimivvva mpan9
        oveq2d oveq12d rcla43v ) ABLZCLZIMZDLZJMZUGUJJMZUHUJJMZIMZNZDHOCHOBHOEH
        PFHPGHPUAEFIMZGJMZEGJMZFGJMZIMZNZAUOBCDHHHKUBUOVAEUHIMZUJJMZEUJJMZUMIMZ
        NUPUJJMZVDFUJJMZIMZNBCDEFGHHHUGENZUKVCUNVEVIUIVBUJJUGEUHIQRVIULVDUMIUGE
        UJJQRSUHFNZVCVFVEVHVJVBUPUJJUHFEITRVJUMVGVDIUHFUJJQUDSUJGNZVFUQVHUTUJGU
        PJTVKVDURVGUSIUJGEJTUJGFJTUESUFUC $.
        $( [19-Oct-2014] $)
    $}

    ${
      caovdi.1 $e |- A e. _V $.
      caovdi.2 $e |- B e. _V $.
      caovdi.3 $e |- C e. _V $.
      caovdi.4 $e |- ( x G ( y F z ) ) = ( ( x G y ) F ( x G z ) ) $.
      $( Convert an operation distributive law to class notation.  (Revised by
         Mario Carneiro, 28-Jun-2013.) $)
      caovdi $p |- ( A G ( B F C ) ) = ( ( A G B ) F ( A G C ) ) $=
        ( cvv wcel co wceq wtru w3a tru cv wa a1i caovdig mpan mp3an ) DMNZEMNZ
        FMNZDEFGOHODEHODFHOGOPZIJKQUFUGUHRUISQABCDEFMGHATZBTZCTZGOHOUJUKHOUJULH
        OGOPQUJMNUKMNULMNRUALUBUCUDUE $.
        $( [28-Jun-2013] $) $( [25-Aug-1995] $)
    $}

    ${
      caopr.1 $e |- A e. _V $.
      caopr.2 $e |- B e. _V $.
      caopr.3 $e |- C e. _V $.
      caopr.com $e |- ( x F y ) = ( y F x ) $.
      caopr.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
      $( Rearrange arguments in a commutative, associative operation. $)
      caov32 $p |- ( ( A F B ) F C ) = ( ( A F C ) F B ) $=
        ( co caovcom oveq2i caovass 3eqtr4i ) DEFGMZGMDFEGMZGMDEGMFGMDFGMEGMRSD
        GABEFGIJKNOABCDEFGHIJLPABCDFEGHJILPQ $.
        $( [26-Aug-1995] $)

      $( Rearrange arguments in a commutative, associative operation. $)
      caov12 $p |- ( A F ( B F C ) ) = ( B F ( A F C ) ) $=
        ( co caovcom oveq1i caovass 3eqtr3i ) DEGMZFGMEDGMZFGMDEFGMGMEDFGMGMRSF
        GABDEGHIKNOABCDEFGHIJLPABCEDFGIHJLPQ $.
        $( [26-Aug-1995] $)

      $( Rearrange arguments in a commutative, associative operation. $)
      caov31 $p |- ( ( A F B ) F C ) = ( ( C F B ) F A ) $=
        ( co caovass caov12 eqtri caov32 eqtr3i 3eqtr4i ) DFGMEGMZFDEGMZGMZUAFG
        MFEGMZDGMZTDUCGMUBABCDFEGHJILNABCDFEGHJIKLOPABCDEFGHIJKLQFDGMEGMUDUBABC
        FDEGJHIKLQABCFDEGJHILNRS $.
        $( [26-Aug-1995] $)

      $( Rearrange arguments in a commutative, associative operation. $)
      caov13 $p |- ( A F ( B F C ) ) = ( C F ( B F A ) ) $=
        ( co caov31 caovass 3eqtr3i ) DEGMFGMFEGMDGMDEFGMGMFEDGMGMABCDEFGHIJKLN
        ABCDEFGHIJLOABCFEDGJIHLOP $.
        $( [26-Aug-1995] $)

      ${
        caopr.4 $e |- D e. _V $.
        $( Rearrange arguments in a commutative, associative operation. $)
        caov4 $p |- ( ( A F B ) F ( C F D ) ) = ( ( A F C ) F ( B F D ) ) $=
          ( co caov12 oveq2i ovex caovass 3eqtr4i ) DEFGHOZHOZHODFEGHOZHOZHODEH
          OUAHODFHOUCHOUBUDDHABCEFGHJKNLMPQABCDEUAHIJFGHRMSABCDFUCHIKEGHRMST $.
          $( [26-Aug-1995] $)

        $( Rearrange arguments in a commutative, associative operation. $)
        caov411 $p |- ( ( A F B ) F ( C F D ) ) = ( ( C F B ) F ( A F D ) ) $=
          ( co caov31 oveq1i ovex caovass 3eqtr3i ) DEHOZFHOZGHOFEHOZDHOZGHOUAF
          GHOHOUCDGHOHOUBUDGHABCDEFHIJKLMPQABCUAFGHDEHRKNMSABCUCDGHFEHRINMST $.
          $( [26-Aug-1995] $)

        $( Rearrange arguments in a commutative, associative operation. $)
        caov42 $p |- ( ( A F B ) F ( C F D ) ) =
                        ( ( A F C ) F ( D F B ) ) $=
          ( co caov4 caovcom oveq2i eqtri ) DEHOFGHOHODFHOZEGHOZHOTGEHOZHOABCDE
          FGHIJKLMNPUAUBTHABEGHJNLQRS $.
          $( [26-Aug-1995] $)
      $}
    $}

    ${
      caoprd.1 $e |- A e. _V $.
      caoprd.2 $e |- B e. _V $.
      caoprd.3 $e |- C e. _V $.
      caoprd.com $e |- ( x G y ) = ( y G x ) $.
      caoprd.distr $e |- ( x G ( y F z ) ) = ( ( x G y ) F ( x G z ) ) $.
      $( Reverse distributive law. $)
      caovdir $p |- ( ( A F B ) G C ) = ( ( A G C ) F ( B G C ) ) $=
        ( co caovdi ovex caovcom oveq12i 3eqtr3i ) FDEGNZHNFDHNZFEHNZGNTFHNDFHN
        ZEFHNZGNABCFDEGHKIJMOABFTHKDEGPLQUAUCUBUDGABFDHKILQABFEHKJLQRS $.
        $( [26-Aug-1995] $)

      ${
        $d x y z H $.  $d x y z R $.
        caoprdl.4 $e |- D e. _V $.
        caoprdl.5 $e |- H e. _V $.
        caoprdl.ass $e |- ( ( x G y ) G z ) = ( x G ( y G z ) ) $.
        $( Lemma used by real number construction. $)
        caovdilem $p |- ( ( ( A G C ) F ( B G D ) ) G H ) =
                         ( ( A G ( C G H ) ) F ( B G ( D G H ) ) ) $=
          ( co ovex caovdir caovass oveq12i eqtri ) DFISZEGISZHSJISUEJISZUFJISZ
          HSDFJISISZEGJISISZHSABCUEUFJHIDFITEGITQNOUAUGUIUHUJHABCDFJIKMQRUBABCE
          GJILPQRUBUCUD $.
          $( [26-Aug-1995] $)

        ${
          caoprdl2.6 $e |- R e. _V $.
          caoprdl2.com $e |- ( x F y ) = ( y F x ) $.
          caoprdl2.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
          $( Lemma used in real number construction. $)
          caovlem2 $p |- ( ( ( ( A G C ) F ( B G D ) ) G H ) F
                            ( ( ( A G D ) F ( B G C ) ) G R ) ) =
   ( ( A G ( ( C G H ) F ( D G R ) ) ) F ( B G ( ( C G R ) F ( D G H ) ) ) ) $=
            ( co ovex caov42 caovdilem oveq12i caovdi 3eqtr4i ) DFKJUCZJUCZEGKJ
            UCZJUCZIUCZDGHJUCZJUCZEFHJUCZJUCZIUCZIUCUKUPIUCZURUMIUCZIUCDFJUCEGJ
            UCIUCKJUCZDGJUCEFJUCIUCHJUCZIUCDUJUOIUCJUCZEUQULIUCJUCZIUCABCUKUMUP
            URIDUJJUDEULJUDDUOJUDUAUBEUQJUDUEVBUNVCUSIABCDEFGIJKLMNOPQRSUFABCDE
            GFIJHLMQOPNTSUFUGVDUTVEVAIABCDUJUOIJLFKJUDGHJUDPUHABCEUQULIJMFHJUDG
            KJUDPUHUGUI $.
            $( [26-Aug-1995] $)
        $}
      $}
    $}

    ${
      $d x y z w v $.  $d w v S $.  $d w v A $.  $d w v B $.  $d w v F $.
      caovmo.1 $e |- A e. _V $.
      $( Identity element. $)
      caovmo.2 $e |- B e. S $.
      caovmo.dom $e |- dom F = ( S X. S ) $.
      caovmo.3 $e |- -. (/) e. S $.
      caovmo.com $e |- ( x F y ) = ( y F x ) $.
      caovmo.ass $e |- ( ( x F y ) F z ) = ( x F ( y F z ) ) $.
      caovmo.id $e |- ( x e. S -> ( x F B ) = x ) $.
      $( Uniqueness of inverse element in commutative, associative operation
         with identity.  Remark in proof of Proposition 9-2.4 of [Gleason]
         p. 119. $)
      caovmo $p |- E* w ( A F w ) = B $=
        ( vv wcel co wceq wa cv wmo wi wal eleq1 eqeq1d anbi12d mo4 vex caovass
        oveq2 caov12 eqtri oveq1 id eqeq12d vtoclga sylan9eqr ad2ant2rl caovcom
        syl5eq elexi sylan9eq eqtr3d ax-gen mpgbir mpbiri ndmovrcl simprd ancri
        ad2ant2lr syl immoi ax-mp ) DUAZGQZEVOHRZFSZTZDUBZVRDUBVTVSPUAZGQZEWAHR
        ZFSZTZTZVOWASZUCZPUDDVSWEDPWGVPWBVRWDVOWAGUEWGVQWCFVOWAEHUKUFUGUHWHPWFV
        QWAHRZVOWAVPWDWIVOSVRWBVPWDTWIVOWCHRZVOWIEVOWAHRHRWJABCEVOWAHIDUIZPUIZN
        UJABCEVOWAHIWKWLMNULUMWDVPWJVOFHRZVOWCFVOHUKAUAZFHRZWNSZWMVOSAVOGWNVOSZ
        WOWMWNVOWNVOFHUNWQUOUPOUQURVAUSVRWBWIWASVPWDVRWBWIFWAHRZWAVQFWAHUNWBWRW
        AFHRZWAABFWAHFGJVBWLMUTWPWSWASAWAGWNWASZWOWSWNWAWNWAFHUNWTUOUPOUQVAVCVK
        VDVEVFVRVSDVRVPVRVQGQZVPVRXAFGQJVQFGUEVGXAEGQVPEVOGHWKKLVHVIVLVJVMVN $.
        $( [4-Mar-1996] $)
    $}
  $}

  ${
    $d v w x y z B $.  $d v w y z N $.  $d v w x O $.  $d v w x y z P $.
    $d v x y z ph $.  $d w x y z v $.
    grplinvlem1.c $e |- ( ( ph /\ x e. B /\ y e. B ) -> ( x P y ) e. B ) $.
    grplinvlem1.o $e |- ( ph -> O e. B ) $.
    grplinvlem1.i $e |- ( ( ph /\ x e. B ) -> ( x P O ) = x ) $.
    grplinvlem1.a $e |- ( ( ph /\ ( x e. B /\ y e. B /\ z e. B ) )
          -> ( ( x P y ) P z ) = ( x P ( y P z ) ) ) $.
    grplinvlem1.n $e |- ( ( ph /\ x e. B ) -> N e. B ) $.
    grplinvlem1.j $e |- ( ( ph /\ x e. B ) -> ( x P N ) = O ) $.
    ${
      $d w y z C $.
      grplinvlem1.w $e |- ( w e. C -> A. x w e. C ) $.
      grplinvlem1.v $e |- C e. _V $.
      $( Lemma for ~ grplinvd . $)
      grplinvlem1 $p |- ( ( ph /\ C e. B /\ ( C P C ) = C ) -> C = O ) $=
        ( wcel co wceq w3a wa simpr wi ax-17 hbcsb1 cv hbel hbim csbeq1a eleq1d
        csb imbi2d expcom vtoclgaf impcom simpl hbov oveq1 oveq1d eqeq12d oveq2
        hbeq oveq2d vtocl3gaf syl31anc id oveq12d eqeq1d 3eqtrrd 3adant3 3eqtrd
        imp 3ad2ant3 ) AGFSZGGHTZGUAZUBGVQBGIUMZHTZGVSHTZJAVPGVTUAVRAVPUCZVTGWA
        HTZGJHTZGWBVPVPVSFSZAVTWCUAZAVPUDZWGVPAWEAIFSZUEAWEUEBEGFQAWEBABUFZBEEV
        SFBEGIRQUGZEUHZFSBUFUIUJBUHZGUAZWHWEAWMIVSFBGIUKZULUNAWLFSZWHOUOUPUQAVP
        URVPVPWEUBAWFAWLCUHZHTZDUHZHTZWLWPWRHTZHTZUAZUEAGWPHTZWRHTZGWTHTZUAZUEA
        VQWRHTZGGWRHTZHTZUAZUEZAWFUEZBCDEGGVSFFFQWKGSZCUFZXMDUFZXNXOWKVSSDUFAXF
        BWIBEEXDXEBEXCWRHBEGWPHQWKHSBUFZWKWPSBUFUSXPWKWRSBUFUSBEGWTHQXPWKWTSBUF
        USVDUJXKCUFXLDUFWMXBXFAWMWSXDXAXEWMWQXCWRHWLGWPHUTVAWLGWTHUTVBUNWPGUAZX
        FXJAXQXDXGXEXIXQXCVQWRHWPGGHVCVAXQWTXHGHWPGWRHUTVEVBUNWRVSUAZXJWFAXRXGV
        TXIWCWRVSVQHVCXRXHWAGHWRVSGHVCVEVBUNAWOWPFSWRFSUBXBNUOVFVNVGWBWAJGHVPAW
        AJUAZAWLIHTZJUAZUEAXSUEBEGFQAXSBWIBEEWAJBEGVSHQXPWJUSWKJSBUFZVDUJWMYAXS
        AWMXTWAJWMWLGIVSHWMVHZWNVIVJUNAWOYAPUOUPUQZVEVPAWDGUAZAWLJHTZWLUAZUEAYE
        UEBEGFQAYEBWIBEEWDGBEGJHQXPYBUSQVDUJWMYGYEAWMYFWDWLGWLGJHUTYCVBUNAWOYGM
        UOUPUQVKVLVRAVTWAUAVPVQGVSHUTVOAVPXSVRYDVLVM $.
        $( [9-Aug-2013] $)
    $}

    $( Lemma for ~ grplinvd . $)
    grplinvlem2 $p |- ( ( ph /\ v e. B ) -> ( [_ v / x ]_ N P v ) = O ) $=
      ( vw wcel co wceq ax-17 cv wa csb simpl wi vex hbcsb1 hbel csbeq1a eleq1d
      hbim imbi2d expcom vtoclgaf impcom simpr hbov oveq1 oveq2 3expb vtocl2gaf
      imp syl21anc w3a hbeq oveq1d eqeq12d oveq2d vtocl3gaf syl31anc id oveq12d
      eqeq1d eqcomd syldan 3eqtr2d eqtr3d ovex grplinvlem1 syl3anc ) AEUAZFQZUB
      ZABWAHUCZWAGRZFQZWEWEGRZWESWEISAWBUDZWCWDFQZWBAWFWBAWIAHFQZUEAWIUEBPWAFPU
      AZWAQZBTZAWIBABTZBPPWDFBPWAHEUFWMUGZWKFQBTZUHUKBUAZWASZWJWIAWRHWDFBWAHUIZ
      UJULAWQFQZWJNUMUNUOZAWBUPZWHWIWBUBAWFAWQCUAZGRZFQZUEAWDXCGRZFQZUEAWFUEZBC
      PWDWAFFWOWKWDQZCTZWLCTZAXGBWNBPPXFFBPWDXCGWOWKGQBTZWKXCQBTZUQZWPUHUKXHCTW
      QWDSZXEXGAXOXDXFFWQWDXCGURZUJULXCWASZXGWFAXQXFWEFXCWAWDGUSZUJULAWTXCFQZUB
      XEAWTXSXEJUTUMVAVBVCZWCWEWDGRZWAGRZWGWEWCWFWIWBAYBWGSZXTXAXBWHWFWIWBVDAYC
      AXDDUAZGRZWQXCYDGRZGRZSZUEZAWEXCGRZYDGRZWEYFGRZSZUEAYAYDGRZWEWDYDGRZGRZSZ
      UEZAYCUEZBCDPWEWDWAFFFBPWDWAGWOXLWMUQZWKWEQZCTUUADTXJXIDTZWLDTZAYMBWNBPPY
      KYLBPYJYDGBPWEXCGYTXLXMUQXLWKYDQBTZUQBPWEYFGYTXLWKYFQBTZUQVEUKYRCTYSDTWQW
      ESZYHYMAUUFYEYKYGYLUUFXDYJYDGWQWEXCGURVFWQWEYFGURVGULXCWDSZYMYQAUUGYKYNYL
      YPUUGYJYAYDGXCWDWEGUSVFUUGYFYOWEGXCWDYDGURVHVGULYDWASZYQYCAUUHYNYBYPWGYDW
      AYAGUSUUHYOWEWEGYDWAWDGUSVHVGULAWTXSYDFQVDYHMUMZVIVBVJWCYAWDWAGWCYAWDWAWD
      GRZGRZWDIGRZWDWCWIWBWIAYAUUKSZXAXBXAWHWIWBWIVDAUUMYIAXFYDGRZWDYFGRZSZUEAW
      EYDGRZWDWAYDGRZGRZSZUEZAUUMUEZBCDPWDWAWDFFFWOXJUUBXKUUCUUBAUUPBWNBPPUUNUU
      OBPXFYDGXNXLUUDUQBPWDYFGWOXLUUEUQVEUKUVACTUVBDTXOYHUUPAXOYEUUNYGUUOXOXDXF
      YDGXPVFWQWDYFGURVGULXQUUPUUTAXQUUNUUQUUOUUSXQXFWEYDGXRVFXQYFUURWDGXCWAYDG
      URVHVGULYDWDSZUUTUUMAUVCUUQYAUUSUUKYDWDWEGUSUVCUURUUJWDGYDWDWAGUSVHVGULUU
      IVIVBVJWCIUUJWDGWCUUJIWBAUUJISZAWQHGRZISZUEAUVDUEBPWAFWMAUVDBWNBPPUUJIBPW
      AWDGWMXLWOUQWKIQBTZVEUKWRUVFUVDAWRUVEUUJIWRWQWAHWDGWRVKWSVLVMULAWTUVFOUMU
      NUOVNVHAWBWIUULWDSZXAWIAUVHAWQIGRZWQSZUEAUVHUEBPWDFWOAUVHBWNBPPUULWDBPWDI
      GWOXLUVGUQWOVEUKXOUVJUVHAXOUVIUULWQWDWQWDIGURXOVKVGULAWTUVJLUMUNUOVOVPVFV
      QABCDPFWEGHIJKLMNOYTWDWAGVRVSVT $.
      $( [10-Aug-2013] $)

    $( Deduce left inverse from right inverse and right identity in an
       associative structure (such as a group). $)
    grplinvd $p |- ( ( ph /\ x e. B ) -> ( N P x ) = O ) $=
      ( vv cv wcel co wceq wi csb csbeq1a eqcomd eqcoms id eqeq1d imbi2d expcom
      oveq12d grplinvlem2 vtoclga impcom ) BPZEQAGUMFRZHSZABOPZGUAZUPFRZHSZTAUO
      TOUMEUPUMSZUSUOAUTURUNHUTUQGUPUMFUQGSUMUPUMUPSGUQBUPGUBUCUDUTUEUIUFUGAUPE
      QUSABCDOEFGHIJKLMNUJUHUKUL $.
      $( [10-Aug-2013] $)

    $( Deduce left identity from right inverse and right identity in an
       associative structure (such as a group). $)
    grplidd $p |- ( ( ph /\ x e. B ) -> ( O P x ) = x ) $=
      ( vw wcel co wceq wi imbi2d vv cv oveq2 id eqeq12d wa csb simpr ax-17 vex
      hbcsb1 hbel hbim csbeq1a eleq1d expcom vtoclgaf impcom simpl oveq1 oveq1d
      w3a oveq2d vtocl3ga syl31anc hbov hbeq oveq12d eqeq1d grplinvlem2 3eqtr3d
      imp vtoclga eqtrd ) BUBZEPZAHVOFQZVORZAHUAUBZFQZVSRZSAVRSUAVOEVSVORZWAVRA
      WBVTVQVSVOVSVOHFUCWBUDUETAVSEPZWAAWCUFZVTVSHFQZVSWDVSBVSGUGZFQZVSFQZVSWFV
      SFQZFQZVTWEWDWCWFEPZWCAWHWJRZAWCUHZWCAWKAGEPZSAWKSBOVSEOUBZVSPBUIZAWKBABU
      IZBOOWFEBOVSGUAUJWPUKZWOEPBUIULUMVOVSRZWNWKAWSGWFEBVSGUNZUOTAVPWNMUPUQURW
      MAWCUSWCWKWCVBAWLAVOCUBZFQZDUBZFQZVOXAXCFQZFQZRZSAVSXAFQZXCFQZVSXEFQZRZSA
      WGXCFQZVSWFXCFQZFQZRZSAWLSBCDVSWFVSEEEWSXGXKAWSXDXIXFXJWSXBXHXCFVOVSXAFUT
      VAVOVSXEFUTUETXAWFRZXKXOAXPXIXLXJXNXPXHWGXCFXAWFVSFUCVAXPXEXMVSFXAWFXCFUT
      VCUETXCVSRZXOWLAXQXLWHXNWJXCVSWGFUCXQXMWIVSFXCVSWFFUCVCUETAVPXAEPXCEPVBXG
      LUPVDVLVEWDWGHVSFWCAWGHRZAVOGFQZHRZSAXRSBOVSEWPAXRBWQBOOWGHBOVSWFFWPWOFPB
      UIWRVFWOHPBUIVGUMWSXTXRAWSXSWGHWSVOVSGWFFWSUDZWTVHVITAVPXTNUPUQURVAWDWIHV
      SFABCDUAEFGHIJKLMNVJVCVKWCAWEVSRZAVOHFQZVORZSAYBSBVSEWSYDYBAWSYCWEVOVSVOV
      SHFUTYAUETAVPYDKUPVMURVNUPVMUR $.
      $( [10-Aug-2013] $)
  $}

