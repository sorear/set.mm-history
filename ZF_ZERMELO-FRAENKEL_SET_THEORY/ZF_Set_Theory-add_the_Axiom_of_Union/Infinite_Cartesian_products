$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
          Infinite Cartesian products
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c X_ $.

  $( Extend class notation to include infinite Cartesian products. $)
  cixp $a class X_ x e. A B $.

  ${
    $d f x $.  $d f A $.  $d f B $.
    $( Definition of infinite Cartesian product of [Enderton] p. 54.  Enderton
       uses a bold "X" with ` x e. A ` written underneath or as a subscript, as
       does Stoll p. 47.  Some books use a capital pi, but we will reserve that
       notation for products of numbers.  Usually ` B ` represents a class
       expression containing ` x ` free and thus can be thought of as
       ` B ( x ) ` .  Normally, ` x ` is not free in ` A ` , although this is
       not a requirement of the definition. $)
    df-ixp $a |- X_ x e. A B =
                 { f | ( f Fn A /\ A. x e. A ( f ` x ) e. B ) } $.
  $}

  ${
    $d f A $.  $d f B $.  $d f x F $.
    $( Membership in an infinite Cartesian product.  See ~ df-ixp for
       discussion of the notation. $)
    elixp2 $p |- ( F e. X_ x e. A B <->
                ( F e. _V /\ F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( vf cvv wcel cixp wa wfn cv cfv wral w3a wceq fneq1 fveq1 eleq1d ralbidv
      anbi12d df-ixp elab2g pm5.32i elex pm4.71ri 3anass 3bitr4i ) DFGZDABCHZGZ
      IUHDBJZAKZDLZCGZABMZIZIUJUHUKUONUHUJUPEKZBJZULUQLZCGZABMZIUPEDUIFUQDOZURU
      KVAUOBUQDPVBUTUNABVBUSUMCULUQDQRSTABCEUAUBUCUJUHDUIUDUEUHUKUOUFUG $.
      $( [28-Sep-2006] $)
  $}

  ${
    $d x F $.
    $( Indexed cross product elements are functions.  (Contributed by Stefan
       O'Rear, 4-Jan-2015.) $)
    ixpfn $p |- ( F e. X_ x e. I J -> F Fn I ) $=
      ( cixp wcel cvv wfn cv cfv wral elixp2 simp2bi ) BACDEFBGFBCHAIBJDFACKACD
      BLM $.
      $( [4-Jan-2015] $)

    elixp.1 $e |- F e. _V $.
    $( Membership in an infinite Cartesian product. $)
    elixp $p |- ( F e. X_ x e. A B <->
                ( F Fn A /\ A. x e. A ( F ` x ) e. B ) ) $=
      ( cvv wcel wfn cv cfv wral w3a wa cixp 3anass elixp2 biantrur 3bitr4i ) D
      FGZDBHZAIDJCGABKZLSTUAMZMDABCNGUBSTUAOABCDPSUBEQR $.
      $( [28-Sep-2006] $)

    ${
      $d x A $.  $d x B $.
      $( Membership in an infinite Cartesian product of a constant ` B ` . $)
      elixpconst $p |- ( F e. X_ x e. A B <-> F : A --> B ) $=
        ( cixp wcel wfn cv cfv wral wa wf elixp ffnfv bitr4i ) DABCFGDBHAIDJCGA
        BKLBCDMABCDENABCDOP $.
        $( [12-Apr-2008] $)
    $}
  $}

  ${
    $d f x A $.  $d f x B $.
    $( Infinite Cartesian product of a constant ` B ` . $)
    ixpconstg $p |- ( ( A e. V /\ B e. W ) -> X_ x e. A B = ( B ^m A ) ) $=
      ( vf wcel cixp cmap co wceq wa cab mapvalg vex elixpconst abbi2i syl6reqr
      cv wf ancoms ) CEGZBDGZABCHZCBIJZKUBUCLUEBCFSZTZFMUDCBEDFNUGFUDABCUFFOPQR
      UA $.
      $( [11-Jan-2015] $)

    ixpconst.1 $e |- A e. _V $.
    ixpconst.2 $e |- B e. _V $.
    $( Infinite Cartesian product of a constant ` B ` . $)
    ixpconst $p |- X_ x e. A B = ( B ^m A ) $=
      ( cvv wcel cixp cmap co wceq ixpconstg mp2an ) BFGCFGABCHCBIJKDEABCFFLM
      $.
      $( [12-Apr-2008] $)
  $}

  ${
    $d f x A $.  $d f x B $.  $d f C $.
    $( Equality theorem for infinite Cartesian product. $)
    ixpeq1 $p |- ( A = B -> X_ x e. A C = X_ x e. B C ) $=
      ( vf wceq cv wfn cfv wcel wral cab cixp fneq2 raleq anbi12d abbidv df-ixp
      wa 3eqtr4g ) BCFZEGZBHZAGUBIDJZABKZSZELUBCHZUDACKZSZELABDMACDMUAUFUIEUAUC
      UGUEUHBCUBNUDABCOPQABDERACDERT $.
      $( [9-Jun-2007] $) $( [29-Sep-2006] $)
  $}

  ${
    $d f A $.  $d f B $.  $d f C $.  $d f x $.
    $( Subclass theorem for infinite Cartesian product. $)
    ss2ixp $p |- ( A. x e. A B C_ C -> X_ x e. A B C_ X_ x e. A C ) $=
      ( vf wss wral cixp cv wfn cfv wcel wa ssel ral2imi anim2d vex elixp ssrdv
      3imtr4g ) CDFZABGZEABCHZABDHZUBEIZBJZAIUEKZCLZABGZMUFUGDLZABGZMUEUCLUEUDL
      UBUIUKUFUAUHUJABCDUGNOPABCUEEQZRABDUEULRTS $.
      $( [29-Sep-2006] $)
  $}

  $( Equality theorem for infinite Cartesian product. $)
  ixpeq2 $p |- ( A. x e. A B = C -> X_ x e. A B = X_ x e. A C ) $=
    ( wss wral wa cixp wceq ss2ixp anim12i eqss ralbii r19.26 bitri 3imtr4i ) C
    DEZABFZDCEZABFZGZABCHZABDHZEZUCUBEZGCDIZABFZUBUCIRUDTUEABCDJABDCJKUGQSGZABF
    UAUFUHABCDLMQSABNOUBUCLP $.
    $( [16-Aug-2007] $) $( [29-Sep-2006] $)

  ${
    $d f x y w $.  $d f x y z $.  $d A f x y $.  $d B f w $.  $d C f z $.
    cbvixp.1 $e |- ( w e. B -> A. y w e. B ) $.
    cbvixp.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbvixp.3 $e |- ( x = y -> B = C ) $.
    $( Change bound variable in an indexed Cartesian product.  (Contributed by
       Jeff Madsen, 20-Jun-2011.) $)
    cbvixp $p |- X_ x e. A B = X_ y e. A C $=
      ( vf cv cfv wcel wral wa cab cixp ax-17 hbel wfn wceq fveq2 cbvral anbi2i
      eleq12d abbii df-ixp 3eqtr4i ) KLZEUAZALZUJMZFNZAEOZPZKQUKBLZUJMZGNZBEOZP
      ZKQAEFRBEGRUPVAKUOUTUKUNUSABEBDDUMFDLUMNBSHTACCURGCLURNASITULUQUBUMURFGUL
      UQUJUCJUFUDUEUGAEFKUHBEGKUHUI $.
      $( [20-Jun-2011] $)
  $}

  ${
    $d A x y f $.  $d B y f z $.  $d C x f z $.
    cbvixpv.1 $e |- ( x = y -> B = C ) $.
    $( Change bound variable in an indexed Cartesian product.  (Contributed by
       Jeff Madsen, 2-Sep-2009.) $)
    cbvixpv $p |- X_ x e. A B = X_ y e. A C $=
      ( vz cv wcel ax-17 cbvixp ) ABGGCDEGHZDIBJLEIAJFK $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d A x $.  $d x y $.
    $( The index variable in an indexed cross product is not free.
       (Contributed by Jeff Madsen, 19-Jun-2011.) $)
    hbixp1 $p |- ( y e. X_ x e. A B -> A. x y e. X_ x e. A B ) $=
      ( cv cixp wcel wfn cfv wral wa vex elixp ax-17 hbra1 hban hbxfrbi ) BEZAC
      DFGRCHZAERIDGZACJZKAACDRBLMSUAASANTACOPQ $.
      $( [19-Jun-2011] $)
  $}

  ${
    $d f x $.  $d f A $.  $d f B $.
    $( A cartesian product of proper-class many sets is empty, because any
       function in the cartesian product has to be a set with domain ` A ` ,
       which is not possible for a proper class domain.  (Contributed by Mario
       Carneiro, 25-Jan-2015.) $)
    ixpprc $p |- ( -. A e. _V -> X_ x e. A B = (/) ) $=
      ( vf cixp c0 wceq cvv wcel wn cv wex neq0 wfn cdm fndm vex dmex syl6eqelr
      ixpfn syl exlimiv sylbi con1i ) ABCEZFGZBHIZUFJDKZUEIZDLUGDUEMUIUGDUIUHBN
      ZUGAUHBCTUJBUHOHBUHPUHDQRSUAUBUCUD $.
      $( [25-Jan-2015] $)
  $}

  ${
    $d f x y A $.  $d f y B $.  $d x y F $.
    $( A member of an infinite Cartesian product maps to the indexed union of
       the product argument.  Remark in [Enderton] p. 54. $)
    ixpf $p |- ( F e. X_ x e. A B -> F : A --> U_ x e. A B ) $=
      ( vy cixp wcel cvv wfn cv cfv wral w3a ciun wf elixp2 ssiun2 sseld ax-17
      wa ralimia anim2i hbiu1 ffnfvf sylibr 3adant1 sylbi ) DABCFGDHGZDBIZAJZDK
      ZCGZABLZMBABCNZDOZABCDPUIUMUOUHUIUMTUIUKUNGZABLZTUOUMUQUIULUPABUJBGCUNUKA
      BCQRUAUBAEBUNDEJZBGASAEBCUCURDGASUDUEUFUG $.
      $( [28-Sep-2006] $)

    $( The union of an infinite Cartesian product is included in a cross
       product. $)
    uniixp $p |- U. X_ x e. A B C_ ( A X. U_ x e. A B ) $=
      ( vy vf cixp cuni ciun cxp cv wcel wf wa wex eluni ixpf eximi sylbi fssxp
      anim2i sseld impcom exlimiv syl ssriv ) DABCFZGZBABCHZIZDJZUGKZUJEJZKZBUH
      ULLZMZENZUJUIKZUKUMULUFKZMZENUPEUJUFOUSUOEURUNUMABCULPTQRUOUQEUNUMUQUNULU
      IUJBUHULSUAUBUCUDUE $.
      $( [28-Sep-2006] $)

    $( The existence of an infinite Cartesian product. ` x ` is normally a
       free-variable parameter in ` B ` .  Remark in Enderton p. 54.  (Revised
       by Mario Carneiro, 25-Jan-2015.) $)
    ixpexg $p |- ( A. x e. A B e. V -> X_ x e. A B e. _V ) $=
      ( cvv wcel wral cixp cuni ciun cxp wss uniixp iunexg xpexg syldan sylancr
      wa ssexg uniexb sylibr wn c0 ixpprc 0ex syl6eqel adantr pm2.61ian ) BEFZC
      DFABGZABCHZEFZUIUJRZUKIZEFZULUMUNBABCJZKZLUQEFZUOABCMUIUJUPEFURABCEDNBUPE
      EOPUNUQESQUKTUAUIUBZULUJUSUKUCEABCUDUEUFUGUH $.
      $( [25-Jan-2015] $) $( [28-Sep-2006] $)
  $}

  ${
    $d f A $.  $d f x $.
    $( An infinite Cartesian product with an empty index set. $)
    ixp0x $p |- X_ x e. (/) A = { (/) } $=
      ( vf c0 cixp cv wfn cfv wcel wral wa cab csn df-ixp wceq elsn fn0 biantru
      ral0 3bitr2i abbi2i eqtr4i ) ADBECFZDGZAFUCHBIZADJZKZCLDMZADBCNUGCUHUCUHI
      UCDOUDUGCDPUCQUFUDUEASRTUAUB $.
      $( [21-Sep-2007] $)
  $}

  ${
    $d A f x $.  $d B f $.  $d C f $.  $d D f $.
    $( An infinite Cartesian product is a subset of set exponentiation.
       (Contributed by Jeff Madsen, 19-Jun-2011.) $)
    ixpssmapg $p |- ( ( A e. C /\ A. x e. A B e. D )
                                -> X_ x e. A B C_ ( U_ x e. A B ^m A ) ) $=
      ( vf wcel wral wa cixp ciun cmap co cv wf ixpf cvv wb iunexg simpl elmapg
      syl2anc syl5ibr ssrdv ) BDGZCEGABHZIZFABCJZABCKZBLMZFNZUHGUKUJGZUGBUIUKOZ
      ABCUKPUGUIQGUEULUMRABCDESUEUFTUIBUKQDUAUBUCUD $.
      $( [19-Jun-2011] $)
  $}

  $( Membership of the empty set in an infinite Cartesian product.
     (Contributed by Steve Rodriguez, 29-Sep-2006.) $)
  0elixp $p |- (/) e. X_ x e. (/) A $=
    ( c0 csn cixp 0ex snid ixp0x eleqtrri ) CCDACBECFGABHI $.
    $( [29-Sep-2006] $)

  ${
    $d f A $.  $d f B $.  $d f x $.
    $( The infinite Cartesian product of a family ` B ( x ) ` with an empty
       member is empty. $)
    ixp0 $p |- ( E. x e. A B = (/) -> X_ x e. A B = (/) ) $=
      ( vf c0 wceq wrex cixp cv wcel wfn cfv wral wa vex elixp wn wb n0i con2i
      reximi rexnal sylib intnand noel pm5.21 sylancl syl5bb eqrdv ) CEFZABGZDA
      BCHZEDIZULJUMBKZAIUMLZCJZABMZNZUKUMEJZABCUMDOPUKURQUSQURUSRUKUQUNUKUPQZAB
      GUQQUJUTABUPUJCUOSTUAUPABUBUCUDUMUEURUSUFUGUHUI $.
      $( [1-Oct-2006] $)
  $}

  ${
    $d f x A $.  $d f B $.
    mapixp.1 $e |- A e. _V $.
    mapixp.2 $e |- B e. _V $.
    ${
      $d x B $.
      $( Express set exponentiation in terms of an infinite Cartesian product.
         Remark in [Stoll] p. 47.  Note that ` B ` is constant, i.e. ` x ` does
         not occur in ` B ` . $)
      mapixp $p |- ( B ^m A ) = X_ x e. A B $=
        ( vf cmap co cixp cv wf wfn cfv wcel wral wa ffnfv elmap vex elixp
        3bitr4i eqriv ) FCBGHZABCIZBCFJZKUEBLAJUEMCNABOPUEUCNUEUDNABCUEQCBUEEDR
        ABCUEFSTUAUB $.
        $( [28-Sep-2006] $)
    $}

    $( An infinite Cartesian product is a subset of set exponentation.  Remark
       in [Enderton] p. 54. $)
    ixpssmap $p |- X_ x e. A B C_ ( U_ x e. A B ^m A ) $=
      ( vf cixp ciun cmap co cv wcel wf ixpf iunex elmap sylibr ssriv ) FABCGZA
      BCHZBIJZFKZSLBTUBMUBUALABCUBNTBUBABCDEODPQR $.
      $( [8-Jul-2007] $) $( [28-Sep-2006] $)
  $}

  ${
    $d A x $.  $d B x $.  $d F x $.
    $( Restriction of an element of an infinite Cartesian product.
       (Contributed by FL, 7-Nov-2011.)  (The proof was shortened by Mario
       Carneiro, 31-May-2014.) $)
    resixp $p |- ( ( B C_ A /\ F e. X_ x e. A C )
       -> ( F |` B ) e. X_ x e. B C ) $=
      ( wss cixp wcel wa cres cvv wfn cfv wral resexg adantl w3a simpr elixp2
      cv sylib simp2d simpl fnssres syl2anc simp3d ssralv eleq1d ralbiia sylibr
      sylc fvres syl3anbrc ) CBFZEABDGZHZIZECJZKHZURCLZATZURMZDHZACNZURACDGHUPU
      SUNECUOOPUQEBLZUNUTUQEKHZVEVAEMZDHZABNZUQUPVFVEVIQUNUPRABDESUAZUBUNUPUCZB
      CEUDUEUQVHACNZVDUQUNVIVLVKUQVFVEVIVJUFVHACBUGUKVCVHACVACHVBVGDVACEULUHUIU
      JACDURSUM $.
      $( [31-May-2014] $) $( [7-Nov-2011] $)
  $}

$(
  @{
    xpixp.1 @e |- P = X_ x e. { (/) , 1o } if ( x = (/) , A , B ) @.
    xpixp.1 @e |- P = X_ x e. 2o if ( x = (/) , A , B ) @.
    xpixp.1 @e |- P =
               X_ x e. { (/) , 1o } ( { <. (/) , A >. , <. 1o , B >. } ` x ) @.
    @( A cross product in terms of an infinite Cartesian product. @)
    xpixp @p |- ( A X. B ) =
                ( ran ( U. P |` { (/) } ) X. ran ( U. P |` { 1o } ) ) @=
?@.
      @( [ ?] @) @( [29-Sep-2006] @)
  @}

  @{
    ixpxp.1 @e |- P = ( A X. B ) @.
    @( An infinite Cartesian product in terms of a cross product. @)
    ixpxp @p |- X_ x e. { (/) , 1o } if ( x = (/) , A , B ) =
                { ( { (/) } X. dom P ) , ( { 1o } X. ran P ) } @=
?@.
      @( [ ?] @) @( [29-Sep-2006] @)
  @}
$)


