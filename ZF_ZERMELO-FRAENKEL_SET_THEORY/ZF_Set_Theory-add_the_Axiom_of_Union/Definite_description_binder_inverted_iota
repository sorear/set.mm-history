$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
        Definite description binder (inverted iota)
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c iota $.
  $( Extend class notation with Russell's definition description binder
     (inverted iota). $)
  cio $a class ( iota x ph ) $.

  ${
    $d w x z $.  $d ph w z $.  $d ph w y $.  $d x y $.
    $( Soundness justification theorem for ~ df-iota .  (Contributed by Andrew
       Salmon, 29-Jun-2011.) $)
    iotajust $p |- U. { y | { x | ph } = { y } } =
       U. { z | { x | ph } = { z } } $=
      ( vw cab cv csn wceq sneq eqeq2d cbvabv eqtri unieqi ) ABFZCGZHZIZCFZODGZ
      HZIZDFZSOEGZHZIZEFUCRUFCEPUDIQUEOPUDJKLUFUBEDUDTIUEUAOUDTJKLMN $.
      $( [12-Jul-2011] $) $( [29-Jun-2011] $)
  $}

  ${
    $d y x $.  $d y ph $.
    $( Define Russell's definition description binder, which can be read as
       "the unique ` x ` such that ` ph ` ," where ` ph ` ordinarily contains
       ` x ` as a free variable.  Our definition is meaningful only when there
       is exactly one ` x ` such that ` ph ` is true (see ~ iotaval );
       otherwise, it evaluates to the empty set (see ~ iotanul ).  Russell used
       the inverted iota symbol ` iota ` to represent the binder. $)
    df-iota $a |- ( iota x ph ) = U. { y | { x | ph } = { y } } $.
  $}

  ${
    $d y x $.  $d y ph $.
    $( Alternate definition for descriptions.  Definition 8.18 in [Quine]
       p. 56.  (Contributed by Andrew Salmon, 30-Jun-2011.) $)
    dfiota2 $p |- ( iota x ph ) = U. { y | A. x ( ph <-> x = y ) } $=
      ( cio cab cv csn wceq cuni wb wal df-iota df-sn eqeq2i abbi bitr4i unieqi
      abbii eqtri ) ABDABEZCFZGZHZCEZIABFUAHZJBKZCEZIABCLUDUGUCUFCUCTUEBEZHUFUB
      UHTBUAMNAUEBOPRQS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d x y z $.  $d z ph $.
    $( Bound-variable hypothesis builder for the ` iota ` class.  (Contributed
       by Andrew Salmon, 11-Jul-2011.) $)
    hbiota1 $p |- ( y e. ( iota x ph ) -> A. x y e. ( iota x ph ) ) $=
      ( vz cio cab cv csn wceq cuni df-iota wcel wa wex eluniab ax-17 hbeq hban
      hbab1 hbex hbxfrbi hbxfreq ) BCABEABFZDGZHZIZDFJZABDKCGZUGLUHUDLZUFMZDNBU
      FDUHOUJBDUIUFBUIBPBDCUCUEABDSUHUELBPQRTUAUB $.
      $( [11-Jul-2011] $)
  $}

  ${
    $d w z ph $.  $d w x z $.  $d w y z $.
    hbiota.1 $e |- ( ph -> A. x ph ) $.
    $( Bound-variable hypothesis builder for the ` iota ` class. $)
    hbiota $p |- ( z e. ( iota y ph ) -> A. x z e. ( iota y ph ) ) $=
      ( vw cio cab cv csn wceq cuni df-iota hbab wcel ax-17 hbeq hbuni hbxfreq
      ) BDACGACHZFIJZKZFHZLACFMBDUCUBBFDBDDTUAABCDENDIUAOBPQNRS $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d w z ps $.  $d w z ph $.  $d w x z $.  $d w y z $.
    hbiotad.1 $e |- ( ph -> A. x ph ) $.
    hbiotad.2 $e |- ( ph -> A. y ph ) $.
    hbiotad.3 $e |- ( ph -> ( ps -> A. x ps ) ) $.
    $( Deduction version of ~ hbiota . $)
    hbiotad $p |- ( ph ->
          ( z e. ( iota y ps ) -> A. x z e. ( iota y ps ) ) ) $=
      ( vw cio cab cv csn wceq cuni df-iota a1i wal alrimi hbabd hbunid hbxfrd
      ax-17 wcel a17d hbeqd ) ACEBDJZBDKZILMZNZIKZOZFUGULNABDIPQACEUKFAUJCIEAAI
      RCFAIUCSACEEUHUIFABCDEAADRCFGSHTAELUIUDCUEUFTUAUB $.
      $( [18-Feb-2013] $)
  $}

  ${
    $d z w x $.  $d z w y $.  $d z w ph $.  $d z w ps $.
    cbviotaf.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    cbviotaf.2 $e |- ( ph -> A. y ph ) $.
    cbviotaf.3 $e |- ( ps -> A. x ps ) $.
    $( Change bound variables in a description binder.  (Contributed by Andrew
       Salmon, 1-Aug-2011.) $)
    cbviotaf $p |- ( iota x ph ) = ( iota y ps ) $=
      ( vw vz cv wceq wb wal cab cuni cio wsbc ax-17 hbbi equequ1 sbequ12 cbval
      hbs1 bibi12d hbsb sbequ sbie syl6bb bitri abbii unieqi dfiota2 3eqtr4i )
      ACJZHJZKZLZCMZHNZOBDJZUOKZLZDMZHNZOACPBDPUSVDURVCHURACIJZQZVEUOKZLZIMVCUQ
      VHCIUQIRVFVGCACIUCVGCRSUNVEKAVFUPVGACIUACIHTUDUBVHVBIDVFVGDACIDFUEVGDRSVB
      IRVEUTKZVFBVGVAVIVFACUTQBAIDCUFABCDGEUGUHIDHTUDUBUIUJUKACHULBDHULUM $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d ph y $.  $d ps x $.
    cbviota.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Change bound variables in a description binder.  (Contributed by Andrew
       Salmon, 1-Aug-2011.) $)
    cbviota $p |- ( iota x ph ) = ( iota y ps ) $=
      ( ax-17 cbviotaf ) ABCDEADFBCFG $.
      $( [1-Aug-2011] $)
  $}

  ${
    $d w z ph $.  $d w z x $.  $d w z y $.
    sb8iota.1 $e |- ( ph -> A. y ph ) $.
    $( Variable substitution in description binder.  Compare ~ sb8eu . $)
    sb8iota $p |- ( iota x ph ) = ( iota y [ y / x ] ph ) $=
      ( vz vw cv wceq wb wal cab cuni wsbc cio ax-17 sb8 equsb3 hbxfrbi dfiota2
      sbbi hbsb hbbi sbequ cbval bibi2i bitri albii 3bitri abbii unieqi 3eqtr4i
      ) ABGEGZHZIZBJZEKZLABCGZMZUQULHZIZCJZEKZLABNURCNUPVBUOVAEUOUNBFGZMZFJUNBU
      QMZCJVAUNBFUNFOPVDVEFCVDABVCMZUMBVCMZICAUMBFTVFVGCABFCDUAVGVCULHZCFBEQVHC
      ORUBRVEFOUNFCBUCUDVEUTCVEURUMBUQMZIUTAUMBCTVIUSURCBEQUEUFUGUHUIUJABESURCE
      SUK $.
      $( [18-Mar-2013] $)
  $}

  ${
    $d y z $.  $d x z $.  $d ph z $.
    $( Equality theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotaeq $p |- ( A. x x = y -> ( iota x ph ) = ( iota y ph ) ) $=
      ( vz cv wceq wal cab csn cuni cio wsbc drsb1 df-clab 3bitr4g eqrdv eqeq1d
      wcel abbidv df-iota unieqd 3eqtr4g ) BECEFBGZABHZDEZIZFZDHZJACHZUFFZDHZJA
      BKACKUCUHUKUCUGUJDUCUDUIUFUCDUDUIUCABUELACUELUEUDRUEUIRABCDMADBNADCNOPQSU
      AABDTACDTUB $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)
  $}

  ${
    $d ph z $.  $d ps z $.  $d x y z $.
    $( Equivalence theorem for descriptions.  (Contributed by Andrew Salmon,
       30-Jun-2011.) $)
    iotabi $p |- ( A. x ( ph <-> ps ) -> ( iota x ph ) = ( iota x ps ) ) $=
      ( vz wb wal cab cv csn wceq cuni abbi biimpi eqeq1d abbidv unieqd df-iota
      cio 3eqtr4g ) ABECFZACGZDHIZJZDGZKBCGZUBJZDGZKACRBCRTUDUGTUCUFDTUAUEUBTUA
      UEJABCLMNOPACDQBCDQS $.
      $( [12-Jul-2011] $) $( [30-Jun-2011] $)

    $( Part of Theorem 8.17 in [Quine] p. 56.  This theorem serves as a lemma
       for the fundamental property of iota.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    uniabio $p |- ( A. x ( ph <-> x = y ) -> U. { x | ph } = y ) $=
      ( cv wceq wb wal cab cuni csn abbi biimpi df-sn syl6eqr unieqd vex syl6eq
      unisn ) ABDCDZEZFBGZABHZISJZISUAUBUCUAUBTBHZUCUAUBUDEATBKLBSMNOSCPRQ $.
      $( [12-Jul-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.19 in [Quine] p. 57.  This theorem is the fundamental property
       of iota.  (Contributed by Andrew Salmon, 11-Jul-2011.) $)
    iotaval $p |- ( A. x ( ph <-> x = y ) -> ( iota x ph ) = y ) $=
      ( vz cv wceq wb wal cio cab cuni dfiota2 cvv wcel wa sbeqalb equcomi syl6
      wi vex ax-mp ex equequ2 eqcoms bibi2d biimpd alimdv com12 alrimiv uniabio
      impbid syl syl5eq ) ABEZCEZFZGZBHZABIAUNDEZFZGZBHZDJKZUOABDLURVBUSUOFZGZD
      HVCUOFURVEDURVBVDURVBVDUOMNZURVBOZVDSCTVFVGUOUSFVDABUOUSMPCDQRUAUBVDURVBV
      DUQVABVDUQVAVDUPUTAUPUTGUOUSCDBUCUDUEUFUGUHUKUIVBDCUJULUM $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Equivalence between two different forms of ` iota ` .  (Contributed by
       Andrew Salmon, 12-Jul-2011.) $)
    iotaequ $p |- ( E! x ph -> ( iota x ph ) = U. { x | ph } ) $=
      ( vz weu cv wceq wb wal wex cio cuni df-eu iotaval uniabio eqtr4d exlimiv
      cab sylbi ) ABDABECEZFGBHZCIABJZABQKZFZABCLTUCCTUASUBABCMABCNOPR $.
      $( [20-Aug-2011] $) $( [12-Jul-2011] $)

    $( Theorem 8.22 in [Quine] p. 57.  This theorem is the result if there
       isn't exactly one ` x ` that satisfies ` ph ` .  (Contributed by Andrew
       Salmon, 11-Jul-2011.) $)
    iotanul $p |- ( -. E! x ph -> ( iota x ph ) = (/) ) $=
      ( vz weu cv wceq wb wal wex cio c0 df-eu wn cuni dfiota2 alnex ax-1 eqidd
      cab impbid1 con2bid alimi abbi dfnul2 syl6eqr sylbir unieqd syl6eq syl5eq
      sylib uni0 sylnbi ) ABDABECEZFGBHZCIZABJZKFABCLUOMZUPUNCSZNZKABCOUQUSKNKU
      QURKUQUNMZCHZURKFUNCPVAURUMUMFZMZCSZKVAUNVCGZCHURVDFUTVECUTVBUNUTVBUTUTVB
      QUTUMRTUAUBUNVCCUCUJCUDUEUFUGUKUHUIUL $.
      $( [20-Aug-2011] $) $( [11-Jul-2011] $)

    $( Theorem 8.23 in [Quine] p. 58.  This theorem proves the existence of the
       ` iota ` class under our definition.  (Contributed by Andrew Salmon,
       11-Jul-2011.) $)
    iotaex $p |- ( iota x ph ) e. _V $=
      ( vz weu cio cvv wcel cv wceq wb wal wex iotaval eqcomd eximi df-eu isset
      3imtr4i wn c0 iotanul 0ex syl6eqel pm2.61i ) ABDZABEZFGZABHCHZIJBKZCLUHUF
      IZCLUEUGUIUJCUIUFUHABCMNOABCPCUFQRUESUFTFABUAUBUCUD $.
      $( [11-Jul-2011] $)

    $( Theorem *14.22 in [WhiteheadRussell] p. 190.  (Contributed by Andrew
       Salmon, 12-Jul-2011.) $)
    iota4 $p |- ( E! x ph -> [ ( iota x ph ) / x ] ph ) $=
      ( vz weu cv wceq wb wal wex cio df-eu wi bi2 alimi sb2 syl iotaval eqcomd
      wsbc dfsbcq mpbid exlimiv sylbi ) ABDABECEZFZGZBHZCIABABJZSZABCKUGUICUGAB
      UDSZUIUGUEALZBHUJUFUKBAUEMNABCOPUGUDUHFUJUIGUGUHUDABCQRABUDUHTPUAUBUC $.
      $( [12-Jul-2011] $)
  $}

  $( Theorem *14.23 in [WhiteheadRussell] p. 191.  (Contributed by Andrew
     Salmon, 12-Jul-2011.) $)
  iota4an $p |- ( E! x ( ph /\ ps )
            -> [ ( iota x ( ph /\ ps ) ) / x ] ph ) $=
    ( wa weu cio wsbc iota4 wi cvv wcel iotaex simpl sbcth ax-mp wb sbcimg mpbi
    syl ) ABDZCETCTCFZGZACUAGZTCHTAIZCUAGZUBUCIZUAJKZUETCLZUDCUAJABMNOUGUEUFPUH
    TACUAJQORS $.
    $( [7-Sep-2011] $) $( [12-Jul-2011] $)

  ${
    $d x y A $.  $d x V $.  $d x ph $.  $d y ps $.
    iota5.1 $e |- ( ( ph /\ A e. V ) -> ( ps <-> x = A ) ) $.
    $( A method for computing iota. $)
    iota5 $p |- ( ( ph /\ A e. V ) -> ( iota x ps ) = A ) $=
      ( vy wcel wa cv wceq wb wal cio alrimiv wi eqeq2 bibi2d albidv imbi12d
      iotaval vtoclg adantl mpd ) ADEHZIZBCJZDKZLZCMZBCNZDKZUFUICFOUEUJULPZABUG
      GJZKZLZCMZUKUNKZPUMGDEUNDKZUQUJURULUSUPUICUSUOUHBUNDUGQRSUNDUKQTBCGUAUBUC
      UD $.
      $( [17-Sep-2013] $)
  $}

  ${
    $d x ph $.
    iotabidv.1 $e |- ( ph -> ( ps <-> ch ) ) $.
    $( Formula-building deduction rule for iota. $)
    iotabidv $p |- ( ph -> ( iota x ps ) = ( iota x ch ) ) $=
      ( wb wal cio wceq alrimiv iotabi syl ) ABCFZDGBDHCDHIAMDEJBCDKL $.
      $( [20-Aug-2011] $)
  $}

  $( Membership law for descriptions.  (Contributed by Andrew Salmon,
     1-Aug-2011.) $)
  iotacl $p |- ( E! x ph -> ( iota x ph ) e. { x | ph } ) $=
    ( weu cio wsbc cab wcel iota4 iotaex elabsbc sylibr ) ABCABABDZELABFGABHABL
    ABIJK $.
    $( [1-Aug-2011] $)

  $( Property of iota.  Compare ~ euuni . $)
  iota1 $p |- ( E! x ph -> ( ph <-> ( iota x ph ) = x ) ) $=
    ( weu cab cuni cv wceq cio euuni iotaequ eqeq1d bitr4d ) ABCZAABDEZBFZGABHZ
    OGABIMPNOABJKL $.
    $( [23-Aug-2011] $)

  $( Membership law for descriptions.  Compare ~ reucl2 . $)
  reiotacl2 $p |- ( E! x e. A ph
                  -> ( iota x ( x e. A /\ ph ) ) e. { x e. A | ph } ) $=
    ( wreu cv wcel wa cio cab crab weu df-reu iotacl sylbi df-rab syl6eleqr ) A
    BCDZBECFAGZBHZRBIZABCJQRBKSTFABCLRBMNABCOP $.
    $( [21-Aug-2011] $)

  ${
    $d x A $.
    $( Membership law for descriptions.  Compare ~ reucl . $)
    reiotacl $p |- ( E! x e. A ph
                    -> ( iota x ( x e. A /\ ph ) ) e. A ) $=
      ( wreu crab cv wcel wa cio wss ssrab2 a1i reiotacl2 sseldd ) ABCDZABCEZCB
      FCGAHBIPCJOABCKLABCMN $.
      $( [21-Aug-2011] $)
  $}

  $( Substitution law for descriptions.  Compare ~ reuunisbc . $)
  reiotasbc $p |- ( E! x e. A ph -> [ ( iota x ( x e. A /\ ph ) ) / x ] ph ) $=
    ( cv wcel wa weu cio wsbc wreu iota4an df-reu ancom eubii bitri wceq iotabi
    wb mpg dfsbcq ax-mp 3imtr4i ) ABDCEZFZBGZABUDBHZIZABCJZABUCAFZBHZIZAUCBKUHU
    IBGUEABCLUIUDBUCAMZNOUJUFPZUKUGRUIUDRUMBUIUDBQULSABUJUFTUAUB $.
    $( [23-Aug-2011] $)

  $( Property of iota.  Compare ~ reuuni1 . $)
  reiota1 $p |- ( ( x e. A /\ E! x e. A ph ) ->
          ( ph <-> ( iota x ( x e. A /\ ph ) ) = x ) ) $=
    ( cv wcel wa wreu cio wceq ibar weu wb df-reu iota1 sylbi sylan9bb ) BDZCEZ
    ARAFZABCGZSBHQIZRAJTSBKSUALABCMSBNOP $.
    $( [23-Aug-2011] $)

  ${
    $d y ph $.  $d y ps $.  $d x y A $.  $d y B $.
    reiota2df.1 $e |- ( ph -> A. x ph ) $.
    reiota2df.2 $e |- ( ph -> ( y e. B -> A. x y e. B ) ) $.
    reiota2df.3 $e |- ( ( ph /\ B e. A ) -> ( ch -> A. x ch ) ) $.
    reiota2df.4 $e |- ( ph -> ( x = B -> ( ps <-> ch ) ) ) $.
    $( Deduction version of ~ reiota2f . $)
    reiota2df $p |- ( ( ph /\ B e. A /\ E! x e. A ps ) ->
            ( ch <-> ( iota x ( x e. A /\ ps ) ) = B ) ) $=
      ( wcel wa wceq wb wi wal alrimi adantr a1i wreu cio ax-17 a1d hbeld hband
      cv a17d anabsi5 hbreu1 hbiota1 hbeqd hbbid hbimd eleq1 adantl imp bibi12d
      eqeq2 imbi2d imbi12d reiota1 ax-gen simpr vtoclgft syl221anc pm2.43d 3imp
      ex ) AGFLZBDFUAZCDUGZFLZBMZDUBZGNZOZAVJVKVQPZAVJVJVRPZAVJMZEUGZGLZWBDQPZE
      QZDQZVSVSDQPZDQVLGNZVMVKBVOVLNZOZPZPZVSOZPZDQZWKDQZVJVSAWEVJAWDDHAWCEAEUC
      IRRSVTWFDAVJVTDQAAVJDAADQAHUDADEGFHIAWAFLDUHUEZUFUIZVTVJVRDWQAVJVJDQPVJWP
      SVTVKVQDWQVKVKDQPVTBDFUJTVTCVPDWQJAVPVPDQPVJADEEVOGHWAVOLZWRDQPAVNDEUKTIU
      LSUMUNUNRAWNVJAWMDHAWGWLAWGMZVMVJWJVRWGVMVJOAVLGFUOUPWSWIVQVKWSBCWHVPAWGB
      COKUQWGWHVPOAVLGVOUSUPURUTVAVIRSWOVTWKDVMVKWIBDFVBVIVCTAVJVDWKVSDEGFVEVFV
      IVGVH $.
      $( [17-Feb-2013] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d y B $.
    reiota2f.1 $e |- ( y e. B -> A. x y e. B ) $.
    reiota2f.2 $e |- ( B e. A -> ( ps -> A. x ps ) ) $.
    reiota2f.3 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  The second
       hypothesis is a weakened bound variable condition that allows ~ hbsbc1g
       to be used.  Compare ~ reuuni2f . $)
    reiota2f $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( wcel wreu cv wa cio wceq wb wi ax-17 wal a1i hbreu1 hbiota1 hbbid hbimd
      hbel hbeq hbim1 eleq1 eqeq2 bibi12d imbi2d imbi12d reiota1 ex vtoclgf imp
      pm2.43i ) FEJZACEKZBCLZEJZAMZCNZFOZPZURUSVEQZVAUSAVCUTOZPZQZQURVFQCDFEGUR
      VFCCDDFEGDLEJCRUEZURUSVECVJUSUSCSQURACEUATURBVDCVJHVDVDCSQURCDDVCFVBCDUBG
      UFTUCUDUGUTFOZVAURVIVFUTFEUHVKVHVEUSVKABVGVDIUTFVCUIUJUKULVAUSVHACEUMUNUO
      UQUP $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d y ph $.  $d x y A $.  $d x y B $.  $d x ps $.
    reiota2.1 $e |- ( x = B -> ( ph <-> ps ) ) $.
    $( A condition that allows us to represent "the unique element in ` A `
       such that ` ph ` " with a class expression ` B ` .  Compare
       ~ reuuni2 . $)
    reiota2 $p |- ( ( B e. A /\ E! x e. A ph ) ->
            ( ps <-> ( iota x ( x e. A /\ ph ) ) = B ) ) $=
      ( vy cv wcel ax-17 wal wi a1i reiota2f ) ABCGDEGHEICJBBCKLEDIBCJMFN $.
      $( [23-Aug-2011] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d y ph $.  $d y ps $.
    $( Restriction of a unique element to a smaller class.  Compare
       ~ reuuniss2 . $)
    reiotass2 $p |- ( ( ( A C_ B /\ A. x e. A ( ph -> ps ) ) /\ ( E. x e. A ph
           /\ E! x e. B ps ) )
         -> ( iota x ( x e. A /\ ph ) ) = ( iota x ( x e. B /\ ps ) ) ) $=
      ( vy wss wi wral wa wrex wreu cv wcel cio wsbc wceq reuss2 simplr syl mpd
      reiotasbc reiotacl ra4sbc sbcimg sylibd mpid sylc wb ssel ad2antrr simprr
      hbiota1 hbsbc1g sbceq1a reiota2f syl2anc mpbid eqcomd ) DEGZABHZCDIZJZACD
      KZBCELZJZJZCMZENBJCOZVHDNAJZCOZVGBCVKPZVIVKQZVGACDLZVBVLABCDERZUTVBVFSVNV
      BACVKPZVLACDUBVNVKDNZVBVPVLHZHACDUCZVQVBVACVKPVRVACVKDUDABCVKDUEUFTUGUHVG
      VKENZVEVLVMUIVGVQVTVGVNVQVOVSTUTVQVTHVBVFDEVKUJUKUAVCVDVEULBVLCFEVKVJCFUM
      ZBCFVKEWAUNBCVKUOUPUQURUS $.
      $( [21-Aug-2011] $)
  $}

  ${
    $d F x y $.  $d A x y $.
    $( A definition of function value in terms of iota.  (Contributed by Scott
       Fenton, 19-Feb-2013.) $)
    dffv3 $p |- ( F ` A ) = ( iota x x e. ( F " { A } ) ) $=
      ( vy cv csn cima wcel cab wceq cuni cio abid2 eqeq1i abbii unieqi df-iota
      cfv df-fv 3eqtr4ri ) AECBFGZHZAIZDEFZJZDIZKUAUDJZDIZKUBALBCRUFUHUEUGDUCUA
      UDAUAMNOPUBADQDBCST $.
      $( [19-Feb-2013] $)
  $}

  ${
    $d y x F $.  $d y x A $.
    fv4.1 $e |- A e. _V $.
    $( Alternate definition of the value of a function.  The value of a
       function expressed using ` iota ` .  (Contributed by Andrew Salmon,
       1-Aug-2011.) $)
    fv4 $p |- ( F ` A ) = ( iota x A F x ) $=
      ( vy cfv cv wbr wceq wb wal cab cuni cio fv2 dfiota2 eqtr4i ) BCFBAGZCHZR
      EGIJAKELMSANEABCDOSAEPQ $.
      $( [1-Aug-2011] $)
  $}


