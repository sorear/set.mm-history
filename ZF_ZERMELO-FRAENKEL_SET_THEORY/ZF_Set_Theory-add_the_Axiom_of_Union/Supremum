$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
           Supremum
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( Add new constant symbol. $)
  $c sup $.  $( Supremum. $)

  $( Extend class notation to include supremum of class ` A ` .  Here ` R ` is
     ordinarily a relation that strictly orders class ` B ` .  For example,
     ` R ` could be 'less than' and ` B ` could be the set of real numbers. $)
  csup $a class sup ( A , B , R ) $.

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.
    $( Define the supremum of class ` A ` .  It is meaningful when ` R ` is a
       relation that strictly orders ` B ` and when the supremum exists.  For
       example, ` R ` could be 'less than', ` B ` could be the set of real
       numbers, and ` A ` could be the set of all positive reals whose square
       is less than 2; in this case the supremum is defined as the square root
       of 2 per ~ sqrval .  See ~ dfsup2 for alternate definition not requiring
       dummy variables.

       We will also use this notation for "infimum" by replacing ` R ` with
       ` ``' R ` . $)
    df-sup $a |- sup ( A , B , R ) = U. { x e. B | ( A. y e. A -. x R y /\
                  A. y e. B ( y R x -> E. z e. A y R z ) ) } $.

    $( can't be used if A is a proper class... important?  we already
       have U. as sup for On... but since sup exists, can't use sup for
       On anyway e.g. sup of On itself.
    df-supNEW $a |- sup = { <. <. x , y >. , z >. | z =
             U. { w e. ( 1st ` x ) | ( A. v e. y -. w ( 2nd ` x ) v /\
                  A. v e. ( 1st ` x ) ( v ( 2nd ` x ) w ->
                 E. u e. y v ( 2nd ` x ) u ) ) } } $.
    $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.
    $( Quantifier free definition of supremum.  (Contributed by Scott Fenton,
       19-Feb-2013.) $)
    dfsup2 $p |- sup ( B , A , R ) =
      U. ( A \ ( ( `' R " B ) u. ( R " ( A \ ( `' R " B ) ) ) ) ) $=
      ( vx vy vz cv wbr wn wral wrex wi wa cdif cin cvv wcel vex elima bitri wb
      csup crab cuni ccnv cima cun df-sup dfrab2 incom eqtri wceq abeq1 mpbiran
      cab eldif wo dfrex2 orbi12i elun ianor 3bitr4i brcnv notbii ralbii impexp
      con2bii imbi1i rexbii imbi2i con34b bitr3i ralbii2 3bitr2ri mpgbir ineq2i
      anbi12i invdif 3eqtri unieqi ) BACUBDGZEGZCHZIZEBJZWBWACHZWBFGZCHZFBKZLZE
      AJZMZDAUCZUDACUEZBUFZCAWONZUFZUGZNZUDDEFBACUHWMWSWMAWLDUOZOZAPWRNZOWSWMWT
      AOXAWLDAUIWTAUJUKWTXBAWTXBULWLWAXBQZUADWLDXBUMXCWAWRQZIZWBWAWNHZIZEBJZWFI
      ZEWPJZMZWLXCWAPQXEWAPWRUPDRZUNXDXKWAWOQZWAWQQZUQXHIZXJIZUQXDXKIXMXOXNXPXM
      XFEBKXOEWAWNBXLSXFEBURTXNWFEWPKXPEWACWPXLSWFEWPURTUSWAWOWQUTXHXJVAVBVGXHW
      EXJWKXGWDEBXFWCWBWACERZXLVCVDVEXIWJEWPAWBAQZWBWOQZIZMZXILXRXTXILZLWBWPQZX
      ILXRWJLXRXTXIVFYCYAXIWBAWOUPVHWJYBXRWJWFXSLYBXSWIWFXSWGWBWNHZFBKWIFWBWNBX
      QSYDWHFBWGWBCFRXQVCVITVJWFXSVKVLVJVBVMVQVNVOVPAWRVRVSVTUK $.
      $( [21-Feb-2013] $) $( [19-Feb-2013] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d R x y z $.
    $( Quantifier-free definition of supremum.  (Contributed by Scott Fenton,
       18-Feb-2013.) $)
    dfsup2OLD $p |- sup ( B , A , R ) =
      U. ( A \ ( ( `' R " B ) u. ( ( R \ ( ( `' R " B ) X. _V ) ) " A ) ) ) $=
      ( vx vy vz cv wbr wn wrex cvv cdif cin wcel vex notbii bitri elima rexbii
      wa csup wral wi crab cuni ccnv cima cxp cun df-sup cab dfrab2 incom abeq1
      wceq wb eldif mpbiran wo elun ioran brcnv ralnex bitr4i brdif brxp 3bitri
      mpbiran2 anbi2i con2bii anbi12i 3bitrri mpgbir ineq2i invdif eqtri 3eqtri
      rexanali unieqi ) BACUADGZEGZCHZIEBUBZWAVTCHZWAFGZCHZFBJZUCEAUBZTZDAUDZUE
      ACUFZBUGZCWLKUHZLZAUGZUIZLZUEDEFBACUJWJWQWJWIDUKZAMAWRMZWQWIDAULWRAUMWSAK
      WPLZMWQWRWTAWRWTUOWIVTWTNZUPDWIDWTUNXAVTWPNZIZVTWLNZIZVTWONZIZTZWIXAVTKNZ
      XCVTKWPUQDOZURXCXDXFUSZIXHXBXKVTWLWOUTPXDXFVAQXEWCXGWHXEWBEBJZIWCXDXLXDWA
      VTWKHZEBJXLEVTWKBXJRXMWBEBWAVTCEOZXJVBSQPWBEBVCVDXGWDWGIZTZEAJZIWHXFXQXFW
      AVTWNHZEAJXQEVTWNAXJRXRXPEAXRWDWAVTWMHZIZTXPWAVTCWMVEXTXOWDXSWGXSWAWLNZWE
      WAWKHZFBJWGXSYAXIWAVTWLKXJVFXJVHFWAWKBXNRYBWFFBWEWACFOXNVBSVGPVIQSQPXQWHW
      DWGEAVRVJVDVKVLVMVNAWPVOVPVQVSVP $.
      $( [18-Feb-2013] $)

  $}

  $( Quantifier-free definition of supremum.  (Contributed by Scott Fenton,
     19-Feb-2013.) $)
  dfsup3OLD $p |- sup ( B , A , R ) =
    U. ( A \ ( ( `' R " B ) u. ( R " ( A \ ( `' R " B ) ) ) ) ) $=
    ( csup ccnv cima cvv cxp cdif cun cuni dfsup2OLD cres crn cin incom 3eqtr4i
    indifcom df-res df-ima difxp1 ineq2i rneqi uneq2i difeq2i unieqi eqtri ) BA
    CDACEBFZCUHGHZIZAFZJZIZKAUHCAUHIZFZJZIZKABCLUMUQULUPAUKUOUHUJAMZNCUNMZNUKUO
    URUSUJAGHZOZCUNGHZOZURUSUTUJOCUTUIIZOVAVCUTCUIRUJUTPVBVDCAUHGUAUBQUJASCUNSQ
    UCUJATCUNTQUDUEUFUG $.
    $( [19-Feb-2013] $)

  ${
    $d x y z R $.  $d x y z A $.  $d x y z B $.  $d x y z C $.
    $( Equality theorem for supremum. $)
    supeq1 $p |- ( B = C -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup raleq df-sup
      rexeq imbi2d ralbidv anbi12d rabbidv unieqd 3eqtr4g ) BCHZEIZFIZDJKZFBLZU
      JUIDJZUJGIDJZGBMZNZFALZOZEAPZQUKFCLZUMUNGCMZNZFALZOZEAPZQBADRCADRUHUSVEUH
      URVDEAUHULUTUQVCUKFBCSUHUPVBFAUHUOVAUMUNGBCUAUBUCUDUEUFEFGBADTEFGCADTUG
      $.
      $( [22-May-1999] $)
  $}

  ${
    supeq1d.1 $e |- ( ph -> B = C ) $.
    $( Equality deduction for supremum.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    supeq1d $p |- ( ph -> sup ( B , A , R ) = sup ( C , A , R ) ) $=
      ( wceq csup supeq1 syl ) ACDGCBEHDBEHGFBCDEIJ $.
      $( [22-Jun-2011] $)
  $}

  ${
    supeq1i.1 $e |- B = C $.
    $( Equality inference for supremum.  (Contributed by Paul Chapman,
       22-Jun-2011.) $)
    supeq1i $p |- sup ( B , A , R ) = sup ( C , A , R ) $=
      ( wceq csup supeq1 ax-mp ) BCFBADGCADGFEABCDHI $.
      $( [22-Jun-2011] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    $( Equality theorem for supremum.  (Contributed by Jeff Madsen,
       2-Sep-2009.) $)
    supeq2 $p |- ( B = C -> sup ( A , B , R ) = sup ( A , C , R ) ) $=
      ( vx vy vz wceq cv wbr wn wral wrex wi wa crab cuni csup rabeq df-sup
      raleq anbi2d rabbidv eqtrd unieqd 3eqtr4g ) BCHZEIZFIZDJKFALZUIUHDJUIGIDJ
      GAMNZFBLZOZEBPZQUJUKFCLZOZECPZQABDRACDRUGUNUQUGUNUMECPUQUMEBCSUGUMUPECUGU
      LUOUJUKFBCUAUBUCUDUEEFGABDTEFGACDTUF $.
      $( [2-Sep-2009] $)
  $}

  ${
    $d y z w u A $.  $d y z w u R $.  $d y z w u B $.  $d x y z w u $.
    hbsup.1 $e |- ( y e. A -> A. x y e. A ) $.
    hbsup.2 $e |- ( y e. B -> A. x y e. B ) $.
    hbsup.3 $e |- ( y e. R -> A. x y e. R ) $.
    $( Hypothesis builder for supremum.  (Contributed by Mario Carneiro,
       20-Mar-2014.) $)
    hbsup $p |- ( y e. sup ( A , B , R ) -> A. x y e. sup ( A , B , R ) ) $=
      ( vz vw vu csup cv wbr wral wcel ax-17 hbel hbbr hbral wn wrex wi wa crab
      cuni df-sup hbn hbrex hbim hban hbrab hbuni hbxfreq ) ABCDELIMZJMZENZUAZJ
      COZUPUOENZUPKMZENZKCUBZUCZJDOZUDZIDUEZUFIJKCDEUGABVGVFAIBDUSVEAURAJCABBUP
      CBMZUPPAQZFRUQAABUOUPEVHUOPAQZHVISUHTVDAJDABBUPDVIGRUTVCAABUPUOEVIHVJSVBA
      KCABBVACVHVAPAQZFRABUPVAEVIHVKSUIUJTUKGULUMUN $.
      $( [20-Mar-2014] $)
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    supmo.1 $e |- R Or A $.
    $( Any class ` B ` has at most one supremum in ` A ` (where ` R ` is
       interpreted as 'less than'). $)
    supmo $p |- E* x ( x e. A /\ ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( vw cv wcel wbr wn wral wrex wi wa wceq breq1 notbid breq2 wmo wal eleq1
      ralbidv imbi1d anbi12d mo4 wb sotrieq2 mpan ad2ant2r cbvralv ralnex bitri
      wor rexbidv imbi12d rcla4va con3d sylan2b an32s adantlrr adantrl rcla4cva
      imp anasss adantrrr adantll mpbir2and ax-gen mpgbir ) AIZDJZVLBIZFKZLZBEM
      ZVNVLFKZVNCIZFKZCENZOZBDMZPZPZAUAWEHIZDJZWFVNFKZLZBEMZVNWFFKZWAOZBDMZPZPZ
      PZVLWFQZOZHUBAWEWOAHWQVMWGWDWNVLWFDUCWQVQWJWCWMWQVPWIBEWQVOWHVLWFVNFRSUDW
      QWBWLBDWQVRWKWAVLWFVNFTUEUDUFUFUGWRHWPWQVLWFFKZLZWFVLFKZLZVMWGWQWTXBPUHZW
      DWNDFUOVMWGPXCGDVLWFFUIUJUKWEWNWTWGWEWMWTWJVMVQWMWTWCVMWMVQWTVQVMWMPZVLVS
      FKZCENZLZWTVQXELZCEMXGVPXHBCEVNVSQZVOXEVNVSVLFTSULXECEUMUNXDXGWTXDWSXFWLW
      SXFOBVLDVNVLQZWKWSWAXFVNVLWFFRXJVTXECEVNVLVSFRUPUQURUSVEUTVAVBVCVCWDWOXBV
      MWCWOXBVQWCWGWJXBWMWCWGWJXBWJWCWGPZWFVSFKZCENZLZXBWJXLLZCEMXNWIXOBCEXIWHX
      LVNVSWFFTSULXLCEUMUNXKXNXBXKXAXMWBXAXMOBWFDVNWFQZVRXAWAXMVNWFVLFRXPVTXLCE
      VNWFVSFRUPUQVDUSVEUTVFVGVHVHVIVJVK $.
      $( [5-May-1999] $)

    $( A supremum is a set. $)
    supex $p |- sup ( B , A , R ) e. _V $=
      ( vx vy vz csup cv wcel wbr wn wral wrex wi wa cab cuni cvv crab eqeltri
      df-sup df-rab unieqi eqtri wmo supmo moabex ax-mp uniex ) BACHZEIZAJULFIZ
      CKLFBMUMULCKUMGICKGBNOFAMPZPZEQZRZSUKUNEATZRUQEFGBACUBURUPUNEAUCUDUEUPUOE
      UFUPSJEFGABCDUGUOEUHUIUJUA $.
      $( [22-May-1999] $)

    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeu $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                  E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( cv wbr wn wral wrex wi wa wcel wmo wreu supmo jctr reu5 sylibr ) AHZBHZ
      FIJBEKUCUBFIUCCHFICELMBDKNZADLZUEUBDOUDNAPZNUDADQUEUFABCDEFGRSUDADTUA $.
      $( [12-Oct-2004] $)

    $( A supremum belongs to its base class (closure law). $)
    supcl $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                sup ( B , A , R ) e. A ) $=
      ( cv wbr wn wral wrex wi wa csup crab cuni df-sup wreu wcel syl syl5eqel
      supeu reucl ) AHZBHZFIJBEKUFUEFIUFCHFICELMBDKNZADLZEDFOUGADPQZDABCEDFRUHU
      GADSUIDTABCDEFGUCUGADUDUAUB $.
      $( [12-Oct-2004] $)

    $( A supremum is an upper bound. $)
    supub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                 ( C e. B -> -. sup ( B , A , R ) R C ) ) $=
      ( vw wcel cv wbr wn wral wrex wi wa wceq breq2 notbid csup crab cuni wreu
      imbi2d df-sup eqcomi wb supcl supeu breq1 ralbidv anbi12d reuuni2 syl2anc
      imbi1d mpbiri simpld rcla4v syl5 vtoclga com12 ) FEJAKZBKZGLZMZBENZVDVCGL
      ZVDCKGLCEOZPZBDNZQZADOZEDGUAZFGLZMZVMVNIKZGLZMZPVMVPPIFEVQFRZVSVPVMVTVRVO
      VQFVNGSTUEVMVNVDGLZMZBENZVQEJVSVMWCVDVNGLZVIPZBDNZVMWCWFQZVLADUBUCZVNRZVN
      WHABCEDGUFUGVMVNDJVLADUDWGWIUHABCDEGHUIABCDEGHUJVLWGADVNVCVNRZVGWCVKWFWJV
      FWBBEWJVEWAVCVNVDGUKTULWJVJWEBDWJVHWDVIVCVNVDGSUPULUMUNUOUQURWBVSBVQEVDVQ
      RWAVRVDVQVNGSTUSUTVAVB $.
      $( [12-Oct-2004] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                      E. z e. B C R z ) ) $=
        ( vw cv wbr wn wral wrex wi wa wcel wceq breq1 rexbidv csup imbi2d crab
        imbi12d cuni df-sup eqcomi wreu supcl supeu notbid ralbidv breq2 imbi1d
        anbi12d reuuni2 syl2anc mpbiri simprd rcla4v syl5 vtoclga com12 imp3a
        wb ) AJZBJZGKZLZBEMZVGVFGKZVGCJZGKZCENZOZBDMZPZADNZFDQZFEDGUAZGKZFVLGKZ
        CENZVSVRWAWCOZVRIJZVTGKZWEVLGKZCENZOZOVRWDOIFDWEFRZWIWDVRWJWFWAWHWCWEFV
        TGSWJWGWBCEWEFVLGSTUDUBVRVGVTGKZVNOZBDMZWEDQWIVRVTVGGKZLZBEMZWMVRWPWMPZ
        VQADUCUEZVTRZVTWRABCEDGUFUGVRVTDQVQADUHWQWSVEABCDEGHUIABCDEGHUJVQWQADVT
        VFVTRZVJWPVPWMWTVIWOBEWTVHWNVFVTVGGSUKULWTVOWLBDWTVKWKVNVFVTVGGUMUNULUO
        UPUQURUSWLWIBWEDVGWERZWKWFVNWHVGWEVTGSXAVMWGCEVGWEVLGSTUDUTVAVBVCVD $.
        $( [13-Oct-2004] $)

      $( Bidirectional form of ~ suplub .  (Contributed by Mario Carneiro,
         6-Sep-2014.) $)
      suplub2 $p |- ( ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) /\
                      B C_ A /\ C e. A ) ->
                    ( C R sup ( B , A , R ) <-> E. z e. B C R z ) ) $=
        ( vw cv wbr wn wral wrex wi wa wcel w3a breq2 mpan csup expdimp 3adant2
        wss suplub cbvrexv wceq wo supub 3ad2ant1 imp wb simpl1 supcl syl simp2
        sselda wor sotric syl2anc con2bid mpbird biimprd simpl3 sotr exp3acom23
        a1i syl3anc jaod mpd rexlimdva syl5bi impbid ) AJZBJZGKLBEMVOVNGKVOCJZG
        KCENOBDMPADNZEDUDZFDQZRZFEDGUAZGKZFVPGKZCENZVQVSWBWDOVRVQVSWBWDABCDEFGH
        UEUBUCWDFIJZGKZIENVTWBWCWFCIEVPWEFGSUFVTWFWBIEVTWEEQZPZWAWEUGZWEWAGKZUH
        ZWFWBOZWHWKWAWEGKZLZVTWGWNVQVRWGWNOVSABCDEWEGHUIUJUKWHWMWKWHWADQZWEDQZW
        MWKLULZWHVQWOVQVRVSWGUMABCDEGHUNUOZVTEDWEVQVRVSUPUQZDGURZWOWPPWQHDWAWEG
        USTUTVAVBWHWIWLWJWIWLOWHWIWBWFWAWEFGSVCVGWHWFWJWBWHVSWPWOWFWJPWBOZVQVRV
        SWGVDWSWRWTVSWPWORXAHDFWEWAGVETVHVFVIVJVKVLVM $.
        $( [6-Sep-2014] $)

      $( An upper bound is not less than the supremum. $)
      supnub $p |- ( E. x e. A ( A. y e. B -. x R y /\
                      A. y e. A ( y R x -> E. z e. B y R z ) ) ->
                    ( ( C e. A /\ A. z e. B -. C R z ) ->
                      -. C R sup ( B , A , R ) ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub expdimp dfrex2 expimpd
        syl6ib con2d ) AIZBIZGJKBELUEUDGJUECIZGJCEMNBDLOADMZFDPZFUFGJZKCELZFEDG
        QGJZKUGUHOZUKUJULUKUICEMZUJKUGUHUKUMABCDEFGHRSUICETUBUCUA $.
        $( [13-Oct-2004] $)
    $}
  $}

  ${
    $d x y z w A $.  $d x y z w R $.  $d x y z w B $.  $d w C $.
    sup.1 $e |- R Or A $.
    sup.2 $e |- E. x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $.
    $( A supremum is unique.  Similar to Theorem I.26 of [Apostol] p. 24 (but
       for suprema in general). $)
    supeui $p |- E! x e. A ( A. y e. B -. x R y /\
                  A. y e. A ( y R x -> E. z e. B y R z ) ) $=
      ( cv wbr wn wral wrex wi wa wreu supeu ax-mp ) AIZBIZFJKBELTSFJTCIFJCEMNB
      DLOZADMUAADPHABCDEFGQR $.
      $( [23-May-1999] $)

    $( A supremum belongs to its base class (closure law). $)
    supcli $p |- sup ( B , A , R ) e. A $=
      ( cv wbr wn wral wrex wi wa csup wcel supcl ax-mp ) AIZBIZFJKBELUATFJUACI
      FJCEMNBDLOADMEDFPDQHABCDEFGRS $.
      $( [23-May-1999] $)

    $( A supremum is an upper bound. $)
    supubi $p |- ( C e. B -> -. sup ( B , A , R ) R C ) $=
      ( cv wbr wn wral wrex wi wa wcel csup supub ax-mp ) AJZBJZGKLBEMUBUAGKUBC
      JGKCENOBDMPADNFEQEDGRFGKLOIABCDEFGHST $.
      $( [23-May-1999] $)

    ${
      $d z C $.
      $( A supremum is the least upper bound. $)
      suplubi $p |- ( ( C e. A /\ C R sup ( B , A , R ) ) ->
                    E. z e. B C R z ) $=
        ( cv wbr wn wral wrex wi wa wcel csup suplub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFEDGRGKPFUCGKCENOIABCDEFGHST $.
        $( [23-May-1999] $)

      $( An upper bound is not less than the supremum. $)
      supnubi $p |- ( ( C e. A /\ A. z e. B -. C R z ) ->
                   -. C R sup ( B , A , R ) ) $=
        ( cv wbr wn wral wrex wi wa wcel csup supnub ax-mp ) AJZBJZGKLBEMUBUAGK
        UBCJZGKCENOBDMPADNFDQFUCGKLCEMPFEDGRGKLOIABCDEFGHST $.
        $( [23-May-1999] $)
    $}
  $}

  ${
    $d A x y z $.  $d R x y z $.  $d B x y z $.
    $( A supremum is unique.  Closed version of ~ supeu .  (Contributed by Jeff
       Madsen, 9-May-2011.) $)
    supeut $p |- ( ( R Or A /\ E. x e. A ( A. y e. B -. x R y /\ A. y e. A
        ( y R x -> E. z e. B y R z ) ) ) -> E! x e. A ( A. y e. B -. x R y /\
                          A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( wor cv wbr wn wral wrex wi wa wreu c0 cif wceq raleq soeq2 anbi2d supeu
      rexeqbi1dv reueqd imbi12d so0 elimhyp dedth imp ) DFGZAHZBHZFIJBEKZULUKFI
      ULCHFICELMZBDKZNZADLZUPADOZUJUQURMUMUNBUJDPQZKZNZAUSLZVAAUSOZMDPDUSRZUQVB
      URVCUPVAADUSVDUOUTUMUNBDUSSUAZUCUPVAADUSVEUDUEABCUSEFUJUSFGPFGDPDUSFTPUSF
      TFUFUGUBUHUI $.
      $( [9-May-2011] $)

    $( A nonempty finite set contains its supremum.  (Contributed by Jeff
       Madsen, 9-May-2011.) $)
    fisup2g $p |- ( ( R Or A /\ ( B e. Fin /\ B =/= (/) /\ B C_ A ) )
                                    -> sup ( B , A , R ) e. B ) $=
      ( vx vy vz wor wcel w3a wa cv wbr wral wrex wi crab cuni wreu a1d supeut
      cfn c0 wne wss csup df-sup wceq simpr3 breq2 rcla4ev ralrimiv anim2d rgen
      wn ex a1i soss fisupg 3ad2antl1 mpdan 3exp com4l 3imp2 reurex syl reximia
      syl6 3syl ssrexv 3ad2ant3 adantl syldan reuuniss2 syl22anc reucl eqeltrrd
      mpd syl5eqel ) ACGZBUAHZBUBUCZBAUDZIZJZBACUEDKZEKZCLUNEBMZWFWECLZWFFKZCLZ
      FBNZOZEAMZJZDAPQZBDEFBACUFWDWGWLEBMZJZDBPQZWOBWDWBWQWNOZDBMZWQDBNZWNDARZW
      RWOUGVSVTWAWBUHWTWDWSDBWEBHZWPWMWGXCWMWPXCWLEAXCWLWFAHXCWHWKWJWHFWEBWIWEW
      FCUIUJUOSUKSULZUMUPWDWQDBRZXAVSVTWAWBXEWBVSVTWAXEWBVSBCGZVTWAXEOOBACUQXFV
      TWAXEXFVTWAIXAXEDEFBCURXFVTXAXEWADEFBBCTUSUTVAVGVBVCZWQDBVDZVEVSWCWNDANZX
      BWDWNDBNZXIWDXEXAXJXGXHWQWNDBXDVFVHWCXJXIOZVSWBVTXKWAWNDBAVIVJVKVQDEFABCT
      VLWQWNDBAVMVNWDXEWRBHXGWQDBVOVEVPVR $.
      $( [9-May-2011] $)
  $}

  ${
    $d x A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    $( A set that contains a greatest element satisfies the antecedent in
       supremum theorems.  This allows ` sup ( A , B , R ) ` to be used in some
       situations without the completeness axiom.  (Contributed by Jeff
       Hoffman, 17-Jun-2008.) $)
    supmaxlem $p |- ( ( C e. A /\ C e. B /\ A. z e. B -. C R z ) -> E. x e. A
         ( A. y e. B -. x R y /\ A. y e. A ( y R x -> E. z e. B y R z ) ) ) $=
      ( wcel cv wbr wn wral wrex wi wa wceq breq2 notbid rcla4ev ralbidv biimpi
      cbvralv ex a1d ralrimiv anim12i ancoms breq1 imbi1d anbi12d sylan2 3impb
      ) FDHZFEHZFCIZGJZKZCELZAIZBIZGJZKZBELZUTUSGJZUTUOGJZCEMZNZBDLZOZADMZUNURO
      UMFUTGJZKZBELZUTFGJZVFNZBDLZOZVJURUNVQURVMUNVPURVMUQVLCBEUOUTPUPVKUOUTFGQ
      RUBUAUNVOBDUNVOUTDHUNVNVFVEVNCFEUOFUTGQSUCUDUEUFUGVIVQAFDUSFPZVCVMVHVPVRV
      BVLBEVRVAVKUSFUTGUHRTVRVGVOBDVRVDVNVFUSFUTGQUITUJSUKUL $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d A x y z $.  $d B x y z $.  $d C x y z $.  $d R x y z $.
    supmax.1 $e |- R Or A $.
    $( The greatest element of a set is its supremum.  Note that the converse
       is not true; the supremum might not be an element of the set
       considered.  (Contributed by Jeff Hoffman, 17-Jun-2008.) $)
    supmax $p |- ( ( C e. A /\ C e. B /\ A. y e. B -. C R y )
                           -> sup ( B , A , R ) = C ) $=
      ( vx vz wcel cv wbr wn wral w3a csup wceq wa wb wrex sylc supmaxlem supcl
      syl simp1 wor sotrieq2 mpan syl2anc simp2 supub 3simpb supnub mpbir2and
      wi ) DBIZDCIZDAJZEKLACMZNZCBEOZDPZUTDEKLZDUTEKLZUSUTBIZUOVAVBVCQRZUSGJZHJ
      ZEKLHCMVGVFEKVGUQEKACSUNHBMQGBSZVDGHABCDEUAZGHABCEFUBUCUOUPURUDBEUEVDUOQV
      EFBUTDEUFUGUHUSVHUPVBVIUOUPURUIGHABCDEFUJTUSVHUOURQVCVIUOUPURUKGHABCDEFUL
      TUM $.
      $( [17-Jun-2008] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z C $.  $d x y z R $.
    suppr.1 $e |- R Or A $.
    $( The supremum of a pair. $)
    suppr $p |- ( ( B e. A /\ C e. A ) -> sup ( { B , C } , A , R ) =
                if ( C R B , B , C ) ) $=
      ( vx vy vz wcel wa cv wbr wn wral wi wceq wb breq2 notbid syl5ibrcom csup
      cpr wrex crab cuni cif df-sup wreu ifcl wmo wo vex elpr wor sonr ad2antrr
      mpan so2nr imnan sylibr ancoms jaod iftrue breq1d adantl sylibrd ad2antlr
      imp simpr iffalse pm2.61dan syl5bi ralrimiv ifpr rcla4ev sylan ex ralbidv
      a1d breq1 imbi1d anbi12d syl12anc supmo jctir reu5 reuuni2 syl2anc syl5eq
      mpbi2and ) BAIZCAIZJZBCUBZADUAFKZGKZDLZMZGWNNZWPWODLZWPHKZDLZHWNUCZOZGANZ
      JZFAUDUEZCBDLZBCUFZFGHWNADUGWMXIWPDLZMZGWNNZWPXIDLZXCOZGANZXGXIPZWMXIAIZX
      FFAUHZXLXOJZXPQXHBCAUIZWMXFFAUCZWOAIXFJFUJZJXRWMYAYBWMXQXLXOYAXTWMXKGWNWP
      WNIWPBPZWPCPZUKZWMXKWPBCGULUMWMXHYEXKOWMXHJZYEBWPDLZMZXKYFYCYHYDYFYHYCBBD
      LZMZWKYJWLXHADUNZWKYJEABDUOUQUPYCYGYIWPBBDRSTYFYHYDBCDLZMZWMXHYMWLWKXHYMO
      ZWLWKJZXHYLJMZYNYKYOYPEACBDURUQXHYLUSUTVAVHYDYGYLWPCBDRSTVBXHXKYHQWMXHXJY
      GXHXIBWPDXHBCVCVDSVEVFWMXHMZJZYECWPDLZMZXKYRYCYTYDYRYTYCYQWMYQVIYCYSXHWPB
      CDRSTYRYTYDCCDLZMZWLUUBWKYQYKWLUUBEACDUOUQVGYDYSUUAWPCCDRSTVBYQXKYTQWMYQX
      JYSYQXICWPDXHBCVJVDSVEVFVKVLVMZWMXNGAWMXNWPAIWMXMXCWMXIWNIXMXCXHBCAAVNXBX
      MHXIWNXAXIWPDRVOVPVQVSVMZXFXSFXIAWOXIPZWSXLXEXOUUEWRXKGWNUUEWQXJWOXIWPDVT
      SVRUUEXDXNGAUUEWTXMXCWOXIWPDRWAVRWBZVOWCFGHAWNDEWDWEXFFAWFUTXFXSFAXIUUFWG
      WHUUCUUDWJWI $.
      $( [17-Jun-2007] $)

    $( The supremum of a singleton. $)
    supsn $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( wcel csn csup wbr cif cpr dfsn2 supeq1i wceq suppr anidms syl5eq syl6eq
      ifid ) BAEZBFZACGZBBCHZBBIZBSUABBJZACGZUCATUDCBKLSUEUCMABBCDNOPUBBRQ $.
      $( [2-Oct-2007] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.  $d x y z R $.
    supsn.1 $e |- R Or A $.
    $( TO BE REVISED - delete this? $)
    $( The supremum of a singleton.  This version of ~ supsn is proved
       directly. $)
    supsnALT $p |- ( B e. A -> sup ( { B } , A , R ) = B ) $=
      ( vx vy vz wcel cv wbr wn wral wrex wi wceq breq2 notbid ralrimiv rcla4ev
      wa csn csup crab cuni df-sup wreu wb wmo elsni wor sonr syl5ibr syl com12
      mpan snidg sylan ex a1d jca breq1 ralbidv imbi1d anbi12d mpdan supmo reu5
      jctir sylibr reuuni2 mpbi2and syl5eq ) BAHZBUAZACUBEIZFIZCJZKZFVNLZVPVOCJ
      ZVPGIZCJZGVNMZNZFALZTZEAUCUDZBEFGVNACUEVMBVPCJZKZFVNLZVPBCJZWCNZFALZWGBOZ
      VMWFEAUFZWJWMTZWNUGVMWFEAMZVOAHWFTEUHZTWOVMWQWRVMWPWQVMWJWMVMWIFVNVPVNHZV
      MWIWSVPBOZVMWINVPBUIVMWIWTBBCJZKZACUJVMXBDABCUKUOWTWHXAVPBBCPQULUMUNRZVMW
      LFAVMWLVPAHVMWKWCVMBVNHWKWCBAUPWBWKGBVNWABVPCPSUQURUSRZUTWFWPEBAVOBOZVSWJ
      WEWMXEVRWIFVNXEVQWHVOBVPCVAQVBXEWDWLFAXEVTWKWCVOBVPCPVCVBVDZSVEEFGAVNCDVF
      VHWFEAVGVIWFWPEABXFVJVEXCXDVKVL $.
      $( [23-May-1999] $)
  $}

