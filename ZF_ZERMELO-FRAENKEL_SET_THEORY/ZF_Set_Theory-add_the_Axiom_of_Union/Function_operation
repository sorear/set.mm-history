$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                             Function operation
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c oF $.
  $c oR $.

  $( Extend class notation to include mapping of an operation to a function
     operation. $)
  cof $a class oF R $.

  $( Extend class notation to include mapping of an binary relation to a
     function relation. $)
  cofr $a class oR R $.

  ${
    $d f g x R $.
    $( Define the function operation map.  The definition is designed so that
       if ` R ` is a binary operation, then ` oF R ` is the analogous operation
       on functions which corresponds to applying ` R ` pointwise to the values
       of the functions. $)
    df-of $a |- oF R = ( f e. _V , g e. _V |->
     ( x e. ( dom f i^i dom g ) |-> ( ( f ` x ) R ( g ` x ) ) ) ) $.

    $( Define the function relation map.  The definition is designed so that if
       ` R ` is a binary relation, then ` oF R ` is the analogous relation on
       functions which is true when each element of the left function relates
       to the corresponding element of the right function. $)
    df-ofr $a |- oR R = { <. f , g >. |
      A. x e. ( dom f i^i dom g ) ( f ` x ) R ( g ` x ) } $.
  $}

  ${
    $d f g x z R $.  $d f g x y z S $.
    $( Equality theorem for function operation.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    ofeq $p |- ( R = S -> oF R = oF S ) $=
      ( vf vg vx wceq cvv cv cdm cin cfv co cmpt cmpt2 cof wcel w3a simp1 oveqd
      df-of mpteq2dv mpt2eq3dva 3eqtr4g ) ABFZCDGGECHZIDHZIJZEHZUEKZUHUFKZALZMZ
      NCDGGEUGUIUJBLZMZNAOBOUDCDGGULUNUDUEGPZUFGPZQZEUGUKUMUQABUIUJUDUOUPRSUAUB
      EACDTEBCDTUC $.
      $( [23-Aug-2014] $) $( [20-Jul-2014] $)

    $( Equality theorem for function relation.  (Contributed by Mario Carneiro,
       28-Jul-2014.) $)
    ofreq $p |- ( R = S -> oR R = oR S ) $=
      ( vx vf vg wceq cv cfv wbr cdm cin wral cofr breq ralbidv opabbidv df-ofr
      copab 3eqtr4g ) ABFZCGZDGZHZUAEGZHZAIZCUBJUDJKZLZDERUCUEBIZCUGLZDERAMBMTU
      HUJDETUFUICUGUCUEABNOPCADEQCBDEQS $.
      $( [28-Jul-2014] $)

    $( A function operation restricted to a set is a set. $)
    ofexg $p |- ( A e. V -> ( oF R |` A ) e. _V ) $=
      ( vz vx vf vg cof wfun wcel cres cvv cv cdm cin cfv co cmpt wceq copab2
      moeq funoprab cmpt2 df-of mpt2v eqtri funeqi mpbir resfunexg mpan ) BHZIZ
      ACJUKAKLJULDMEFMZNGMZNOEMZUMPUOUNPBQRZSZFGDTZIUQFGDDUPUAUBUKURUKFGLLUPUCU
      REBFGUDFGDUPUEUFUGUHUKACUIUJ $.
      $( [28-Jul-2014] $)

    hbof.1 $e |- ( x e. R -> A. y x e. R ) $.
    $( Hypothesis builder for function operation.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    hbof $p |- ( x e. oF R -> A. y x e. oF R ) $=
      ( vf vg vz cof cvv cv cdm cin cfv co cmpt cmpt2 df-of wcel ax-17 hbov
      hbmpt hbmpt2 hbxfreq ) BACHEFIIGEJZKFJZKLZGJZUDMZUGUEMZCNZOZPGCEFQEFBAIIU
      KAJZIRBSZUMBGAUFUJULUFRBSBAUHUICULUHRBSDULUIRBSTUAUBUC $.
      $( [20-Jul-2014] $)

    $( Hypothesis builder for function relation.  (Contributed by Mario
       Carneiro, 28-Jul-2014.) $)
    hbofr $p |- ( x e. oR R -> A. y x e. oR R ) $=
      ( vz vf vg cofr cfv wbr cdm cin wral copab df-ofr wcel ax-17 hbbr hbral
      cv hbopab hbxfreq ) BACHETZFTZIZUCGTZIZCJZEUDKUFKLZMZFGNECFGOUJFGBAUHBEUI
      UCUIPBQBAUEUGCATZUEPBQDUKUGPBQRSUAUB $.
      $( [28-Jul-2014] $)
  $}

  ${
    $d x A $.  $d f g x F $.  $d f g x G $.  $d x ph $.  $d x S $.  $d x X $.
    $d f g x R $.
    offval.1 $e |- ( ph -> F Fn A ) $.
    offval.2 $e |- ( ph -> G Fn B ) $.
    offval.3 $e |- ( ph -> A e. V ) $.
    offval.4 $e |- ( ph -> B e. W ) $.
    offval.5 $e |- ( A i^i B ) = S $.
    ${
      offval.6 $e |- ( ( ph /\ x e. A ) -> ( F ` x ) = C ) $.
      offval.7 $e |- ( ( ph /\ x e. B ) -> ( G ` x ) = D ) $.
      $( Value of an operation applied to two functions.  (Contributed by Mario
         Carneiro, 20-Jul-2014.) $)
      offval $p |- ( ph -> ( F oF R G ) = ( x e. S |-> ( C R D ) ) ) $=
        ( cvv vf vg cof co cdm cin cfv cmpt wcel wceq wfn fnex syl2anc fndm syl
        cv ineq12d syl6eq mpteq1 inex1g syl5eqelr mptexg 3syl eqeltrd ineqan12d
        wa dmeq fveq1 oveqan12d mpteq12dv df-of ovmpt2ga syl3anc eleq2i adantrr
        elin bitr3i adantrl oveq12d sylan2b mpteq2dva 3eqtrd ) AIJGUCZUDZBIUEZJ
        UEZUFZBUPZIUGZWHJUGZGUDZUHZBHWKUHZBHEFGUDZUHAITUIZJTUIZWLTUIWDWLUJAICUK
        ZCKUIZWOMOCKIULUMAJDUKZDLUIWPNPDLJULUMAWLWMTAWGHUJWLWMUJAWGCDUFZHAWECWF
        DAWQWECUJMCIUNUOAWSWFDUJNDJUNUOUQQURBWGHWKUSUOZAWRHTUIWMTUIOWRHWTTQCDKU
        TVABHWKTVBVCVDUAUBIJTTBUAUPZUEZUBUPZUEZUFZWHXBUGZWHXDUGZGUDZUHWLWCTXBIU
        JZXDJUJZVFBXFXIWGWKXJXKXCWEXEWFXBIVGXDJVGVEXJXKXGWIXHWJGWHXBIVHWHXDJVHV
        IVJBGUAUBVKVLVMXAABHWKWNWHHUIZAWHCUIZWHDUIZVFZWKWNUJXLWHWTUIXOWTHWHQVNW
        HCDVPVQAXOVFWIEWJFGAXMWIEUJXNRVOAXNWJFUJXMSVRVSVTWAWB $.
        $( [20-Jul-2014] $)

      $( Value of a relation applied to two functions.  (Contributed by Mario
         Carneiro, 28-Jul-2014.) $)
      ofrfval $p |- ( ph -> ( F oR R G <-> A. x e. S C R D ) ) $=
        ( wcel vf vg cofr wbr cv cfv cdm cin wral cvv wb fnex syl2anc wceq dmeq
        wfn wa ineqan12d fveq1 breqan12d raleqbidv df-ofr brabga ineq12d syl6eq
        fndm raleqdv inss1 eqsstr3i sseli sylan2 inss2 breq12d ralbidva 3bitrd
        syl ) AIJGUCZUDZBUEZIUFZVSJUFZGUDZBIUGZJUGZUHZUIZWBBHUIEFGUDZBHUIAIUJTZ
        JUJTZVRWFUKAICUPZCKTWHMOCKIULUMAJDUPZDLTWINPDLJULUMVSUAUEZUFZVSUBUEZUFZ
        GUDZBWLUGZWNUGZUHZUIWFUAUBIJVQUJUJWLIUNZWNJUNZUQWPWBBWSWEWTXAWQWCWRWDWL
        IUOWNJUOURWTXAWMVTWOWAGVSWLIUSVSWNJUSUTVABGUAUBVBVCUMAWBBWEHAWECDUHZHAW
        CCWDDAWJWCCUNMCIVFVPAWKWDDUNNDJVFVPVDQVEVGAWBWGBHAVSHTZUQVTEWAFGXCAVSCT
        VTEUNHCVSHXBCQCDVHVIVJRVKXCAVSDTWAFUNHDVSHXBDQCDVLVIVJSVKVMVNVO $.
        $( [28-Jul-2014] $)
    $}

    ${
      ofval.6 $e |- ( ( ph /\ X e. A ) -> ( F ` X ) = C ) $.
      ofval.7 $e |- ( ( ph /\ X e. B ) -> ( G ` X ) = D ) $.
      $( Evaluate a function operation at a point.  (Contributed by Mario
         Carneiro, 20-Jul-2014.) $)
      ofval $p |- ( ( ph /\ X e. S ) -> ( ( F oF R G ) ` X ) = ( C R D ) ) $=
        ( cfv vx wcel wa cof co cv cmpt wceq eqidd offval fveq1d adantr oveq12d
        fveq2 eqid ovex fvmpt adantl inss1 eqsstr3i sseli sylan2 inss2 3eqtrd
        cin ) ALGUBZUCZLHIFUDUEZTZLUAGUAUFZHTZVJITZFUEZUGZTZLHTZLITZFUEZDEFUEAV
        IVOUHVFALVHVNAUABCVKVLFGHIJKMNOPQAVJBUBUCVKUIAVJCUBUCVLUIUJUKULVFVOVRUH
        AUALVMVRGVNVJLUHVKVPVLVQFVJLHUNVJLIUNUMVNUOVPVQFUPUQURVGVPDVQEFVFALBUBV
        PDUHGBLGBCVEZBQBCUSUTVARVBVFALCUBVQEUHGCLGVSCQBCVCUTVASVBUMVD $.
        $( [20-Jul-2014] $)

      $( Exhibit a function relation at a point.  (Contributed by Mario
         Carneiro, 28-Jul-2014.) $)
      ofrval $p |- ( ( ph /\ F oR R G /\ X e. S ) -> C R D ) $=
        ( wcel vx cofr wbr w3a cfv wa cv wral eqidd ofrfval biimpa wceq breq12d
        wi fveq2 rcla4cv syl 3impia simp1 cin inss1 eqsstr3i simp3 sseldi inss2
        syl2anc 3brtr3d ) AHIFUBUCZLGTZUDZLHUEZLIUEZDEFAVHVIVKVLFUCZAVHUFUAUGZH
        UEZVNIUEZFUCZUAGUHZVIVMUNAVHVRAUABCVOVPFGHIJKMNOPQAVNBTUFVOUIAVNCTUFVPU
        IUJUKVQVMUALGVNLULVOVKVPVLFVNLHUOVNLIUOUMUPUQURVJALBTVKDULAVHVIUSZVJGBL
        GBCUTZBQBCVAVBAVHVIVCZVDRVFVJALCTVLEULVSVJGCLGVTCQBCVEVBWAVDSVFVG $.
        $( [28-Jul-2014] $)
    $}

    $( The function operation produces a function.  (Contributed by Mario
       Carneiro, 22-Jul-2014.) $)
    offn $p |- ( ph -> ( F oF R G ) Fn S ) $=
      ( vx co wfn cfv cvv wcel cof cv cmpt eqid fnmpt ovex mprg wa eqidd offval
      a1i fneq1d mpbiri ) AFGDUAPZEQOEOUBZFRZUOGRZDPZUCZEQZURSTZUTOEOEURUSSUSUD
      UEVAUOETUPUQDUFUKUGAEUNUSAOBCUPUQDEFGHIJKLMNAUOBTUHUPUIAUOCTUHUQUIUJULUM
      $.
      $( [22-Jul-2014] $)
  $}

  ${
    $d z A $.  $d z C $.  $d y z G $.  $d x y z ph $.  $d x y S $.  $d x y T $.
    $d x y z F $.  $d x y z R $.  $d x y z U $.
    off.1 $e |- ( ( ph /\ ( x e. S /\ y e. T ) ) -> ( x R y ) e. U ) $.
    off.2 $e |- ( ph -> F : A --> S ) $.
    off.3 $e |- ( ph -> G : B --> T ) $.
    off.4 $e |- ( ph -> A e. V ) $.
    off.5 $e |- ( ph -> B e. W ) $.
    off.6 $e |- ( A i^i B ) = C $.
    $( The function operation produces a function.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    off $p |- ( ph -> ( F oF R G ) : C --> U ) $=
      ( vz cof co wf cv cfv cmpt wcel wa wral cin inss1 eqsstr3i ffvelrn syl2an
      sseli inss2 ralrimivva adantr wceq oveq1 eleq1d oveq2 rcla42va eqid fmptd
      syl21anc wfn ffn syl eqidd offval feq1d mpbird ) AFJKLGUBUCZUDFJUAFUAUEZK
      UFZVPLUFZGUCZUGZUDAUAFVSJVTAVPFUHZUIVQHUHZVRIUHZBUEZCUEZGUCZJUHZCIUJBHUJZ
      VSJUHZADHKUDZVPDUHZWBWAPFDVPFDEUKZDTDEULUMUPDHVPKUNUOAEILUDZVPEUHZWCWAQFE
      VPFWLETDEUQUMUPEIVPLUNUOAWHWAAWGBCHIOURUSWGWIVQWEGUCZJUHBCVQVRHIWDVQUTWFW
      OJWDVQWEGVAVBWEVRUTWOVSJWEVRVQGVCVBVDVGVTVEVFAFJVOVTAUADEVQVRGFKLMNAWJKDV
      HPDHKVIVJAWMLEVHQEILVIVJRSTAWKUIVQVKAWNUIVRVKVLVMVN $.
      $( [20-Jul-2014] $)
  $}

  ${
    $d x A $.  $d x C $.  $d x F $.  $d x G $.  $d x ph $.  $d x R $.
    ofres.1 $e |- ( ph -> F Fn A ) $.
    ofres.2 $e |- ( ph -> G Fn B ) $.
    ofres.3 $e |- ( ph -> A e. V ) $.
    ofres.4 $e |- ( ph -> B e. W ) $.
    ofres.5 $e |- ( A i^i B ) = C $.
    $( Restrict the operands of a function operation to the same domain as that
       of the operation itself.  (Contributed by Mario Carneiro,
       15-Sep-2014.) $)
    ofres $p |- ( ph -> ( F oF R G ) = ( ( F |` C ) oF R ( G |` C ) ) ) $=
      ( vx co cfv wcel cvv wfn cof cv cmpt cres eqidd offval wss inss1 eqsstr3i
      wa cin fnssres sylancl inss2 ssexg sylancr inidm wceq fvres adantl eqtr4d
      ) AFGEUAZPODOUBZFQZVCGQZEPUCFDUDZGDUDZVBPAOBCVDVEEDFGHIJKLMNAVCBRUJVDUEAV
      CCRUJVEUEUFAODDVDVEEDVFVGSSAFBTDBUGZVFDTJDBCUKZBNBCUHUIZBDFULUMAGCTDCUGVG
      DTKDVICNBCUNUICDGULUMAVHBHRDSRVJLDBHUOUPZVKDUQVCDRZVCVFQVDURAVCDFUSUTVLVC
      VGQVEURAVCDGUSUTUFVA $.
      $( [15-Sep-2014] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d y z C $.  $d y F $.  $d y G $.  $d x y ph $.
    $d x y z R $.  $d x W $.  $d x X $.
    offval2.1 $e |- ( ph -> A e. V ) $.
    offval2.2 $e |- ( ( ph /\ x e. A ) -> B e. W ) $.
    offval2.3 $e |- ( ( ph /\ x e. A ) -> C e. X ) $.
    offval2.4 $e |- ( ph -> F = ( x e. A |-> B ) ) $.
    offval2.5 $e |- ( ph -> G = ( x e. A |-> C ) ) $.
    $( The function operation expressed as a mapping.  (Contributed by Mario
       Carneiro, 20-Jul-2014.) $)
    offval2 $p |- ( ph -> ( F oF R G ) = ( x e. A |-> ( B R C ) ) ) $=
      ( vy vz cmpt wcel cof co cv cfv wfn eqid fmptd feq1d mpbird ffn syl inidm
      wf wa wceq adantr fveq1d offval hbmpt1 ax-17 hbfv hbov fveq2 cbvmpt simpr
      oveq12d fvmpt2 syl2anc mpteq2dva syl5eq eqtrd ) AGHFUAUBQCQUCZBCDSZUDZVLB
      CESZUDZFUBZSZBCDEFUBZSZAQCCVNVPFCGHIIACJGUMZGCUEAWACJVMUMABCDJVMMVMUFZUGA
      CJGVMOUHUICJGUJUKACKHUMZHCUEAWCCKVOUMABCEKVONVOUFZUGACKHVOPUHUICKHUJUKLLC
      ULAVLCTZUNZVLGVMAGVMUOWEOUPUQWFVLHVOAHVOUOWEPUPUQURAVRBCBUCZVMUDZWGVOUDZF
      UBZSVTQBRCVQWJBRVNVPFBRVLVMBRCDUSRUCZVLTBUTZVAWKFTBUTBRVLVOBRCEUSWLVAVBWK
      WJTQUTVLWGUOVNWHVPWIFVLWGVMVCVLWGVOVCVFVDABCWJVSAWGCTZUNZWHDWIEFWNWMDJTWH
      DUOAWMVEZMBCDJVMWBVGVHWNWMEKTWIEUOWONBCEKVOWDVGVHVFVIVJVK $.
      $( [20-Jul-2014] $)

    $( The function relation acting on maps.  (Contributed by Mario Carneiro,
       20-Jul-2014.) $)
    ofrfval2 $p |- ( ph -> ( F oR R G <-> A. x e. A B R C ) ) $=
      ( vy vz wcel wceq cofr wbr cv cmpt cfv wral wf wfn fmptd feq1d mpbird ffn
      eqid syl inidm adantr fveq1d ofrfval hbmpt1 ax-17 hbfv hbbr fveq2 breq12d
      wa cbvral simpr fvmpt2 syl2anc ralbidva syl5bb bitrd ) AGHFUAUBQUCZBCDUDZ
      UEZVMBCEUDZUEZFUBZQCUFZDEFUBZBCUFZAQCCVOVQFCGHIIACJGUGZGCUHAWBCJVNUGABCDJ
      VNMVNUMZUIACJGVNOUJUKCJGULUNACKHUGZHCUHAWDCKVPUGABCEKVPNVPUMZUIACKHVPPUJU
      KCKHULUNLLCUOAVMCSZVEZVMGVNAGVNTWFOUPUQWGVMHVPAHVPTWFPUPUQURVSBUCZVNUEZWH
      VPUEZFUBZBCUFAWAVRWKQBCBRVOVQFBRVMVNBRCDUSRUCZVMSBUTZVAWLFSBUTBRVMVPBRCEU
      SWMVAVBWKQUTVMWHTVOWIVQWJFVMWHVNVCVMWHVPVCVDVFAWKVTBCAWHCSZVEZWIDWJEFWOWN
      DJSWIDTAWNVGZMBCDJVNWCVHVIWOWNEKSWJETWPNBCEKVPWEVHVIVDVJVKVL $.
      $( [20-Jul-2014] $)
  $}

  ${
    $d y A $.  $d x y C $.  $d x y F $.  $d x y G $.  $d x y H $.  $d x y ph $.
    $d x D $.  $d x y R $.
    ofco.1 $e |- ( ph -> F Fn A ) $.
    ofco.2 $e |- ( ph -> G Fn B ) $.
    ofco.3 $e |- ( ph -> H : D --> C ) $.
    ofco.4 $e |- ( ph -> A e. V ) $.
    ofco.5 $e |- ( ph -> B e. W ) $.
    ofco.6 $e |- ( ph -> D e. X ) $.
    ofco.7 $e |- ( A i^i B ) = C $.
    $( The composition of a function operation with another function.
       (Contributed by Mario Carneiro, 19-Dec-2014.) $)
    ofco $p |- ( ph ->
      ( ( F oF R G ) o. H ) = ( ( F o. H ) oF R ( G o. H ) ) ) $=
      ( cfv vx vy cof co ccom cv cmpt wf wcel ffvelrn sylan wfn wceq ffn dffn5v
      syl sylib wa eqidd offval fveq2 oveq12d fmptco wss cin inss1 eqsstr3i fss
      sylancl fnfco syl2anc inss2 inidm fvco2 eqtr4d ) AGHFUCZUDZIUEUAEUAUFZITZ
      GTZVSHTZFUDZUGGIUEZHIUEZVPUDAUAUBEDVSUBUFZGTZWEHTZFUDWBIVQAEDIUHZVREUIZVS
      DUIOEDVRIUJUKAIEULZIUAEVSUGUMAWHWJOEDIUNUPZUAEIUOUQAUBBCWFWGFDGHJKMNPQSAW
      EBUIURWFUSAWECUIURWGUSUTWEVSUMWFVTWGWAFWEVSGVAWEVSHVAVBVCAUAEEVTWAFEWCWDL
      LAGBULEBIUHZWCEULMAWHDBVDWLODBCVEZBSBCVFVGEDBIVHVIBEGIVJVKAHCULECIUHZWDEU
      LNAWHDCVDWNODWMCSBCVLVGEDCIVHVICEHIVJVKRREVMAWJWIVRWCTVTUMWKEGIVRVNUKAWJW
      IVRWDTWAUMWKEHIVRVNUKUTVO $.
      $( [19-Dec-2014] $)
  $}

  ${
    $d x A $.  $d x F $.  $d x G $.  $d x H $.  $d x ph $.  $d x R $.
    offveq.1 $e |- ( ph -> A e. V ) $.
    offveq.2 $e |- ( ph -> F Fn A ) $.
    offveq.3 $e |- ( ph -> G Fn A ) $.
    offveq.4 $e |- ( ph -> H Fn A ) $.
    offveq.5 $e |- ( ( ph /\ x e. A ) -> ( F ` x ) = B ) $.
    offveq.6 $e |- ( ( ph /\ x e. A ) -> ( G ` x ) = C ) $.
    ${
      offveq.7 $e |- ( ( ph /\ x e. A ) -> ( B R C ) = ( H ` x ) ) $.
      $( Convert an identity of the operation to the analogous identity on the
         function operation.  (Contributed by Mario Carneiro, 24-Jul-2014.) $)
      offveq $p |- ( ph -> ( F oF R G ) = H ) $=
        ( cof co cfv inidm offn cv wcel wa ofval eqtrd eqfnfvd ) ABCGHFRSZIACCF
        CGHJJLMKKCUAZUBNABUCZCUDUEUKUITDEFSUKITACCDEFCGHJJUKLMKKUJOPUFQUGUH $.
        $( [24-Jul-2014] $)
    $}

    $d y A $.  $d y z B $.  $d y z C $.  $d x y z F $.  $d y z G $.  $d y H $.
    $d y R $.  $d y ph $.
    $( Equivalent expressions for equality with a function operation. $)
    offveqb $p |- ( ph
          -> ( H = ( F oF R G ) <-> A. x e. A ( H ` x ) = ( B R C ) ) ) $=
      ( vy vz wceq cfv cof co cv wral wcel w3a fveq1 3ad2ant2 inidm ofval eqtrd
      3adant2 3exp ralrimdv wa csb adantr wfn wi ax-17 wel hbcsb1 hbeq hbim weq
      eleq1 anbi2d fveq2 csbeq1a eqeq12d imbi12d chvar adantlr ra4sbc impcom wb
      wsb cvv vex sbceqg ax-mp csbfvg eqeq12i bitri sylib eqcomd adantll offveq
      csbov12g ex impbid ) AIGHFUAUBZSZBUCZITZDEFUBZSZBCUDZAWMWQBCAWMWNCUEZWQAW
      MWSUFWOWNWLTZWPWMAWOWTSWSWNIWLUGUHAWSWTWPSWMACCDEFCGHJJWNLMKKCUIOPUJULUKU
      MUNAWRWMAWRUOZWLIXAQCBQUCZDUPZBXBEUPZFGHIJACJUEWRKUQAGCURWRLUQAHCURWRMUQA
      ICURWRNUQAXBCUEZXBGTZXCSZWRAWSUOZWNGTZDSZUSAXEUOZXGUSBQXKXGBXKBUTZBRRXFXC
      RUCZXFUEBUTBRXBDRQVABUTZVBVCVDBQVEZXHXKXJXGXOWSXEAWNXBCVFVGZXOXIXFDXCWNXB
      GVHBXBDVIVJVKOVLVMAXEXBHTZXDSZWRXHWNHTZESZUSXKXRUSBQXKXRBXLBRRXQXDXMXQUEB
      UTBRXBEXNVBVCVDXOXHXKXTXRXPXOXSXQEXDWNXBHVHBXBEVIVJVKPVLVMWRXEXCXDFUBZXBI
      TZSAWRXEUOZYBYAYCWQBQVQZYBYASZXEWRYDWQBXBCVNVOYDBXBWOUPZBXBWPUPZSZYEXBVRU
      EZYDYHVPQVSZBXBWOWPVRVTWAYFYBYGYAYIYFYBSYJBXBVRIWBWAYIYGYASYJBXBDEVRFWIWA
      WCWDWEWFWGWHWFWJWK $.
      $( [9-Oct-2014] $)
  $}

  ${
    ofc1.1 $e |- ( ph -> A e. V ) $.
    ofc1.2 $e |- ( ph -> B e. W ) $.
    ofc1.3 $e |- ( ph -> F Fn A ) $.
    ofc1.4 $e |- ( ( ph /\ X e. A ) -> ( F ` X ) = C ) $.
    $( Left operation by a constant.  (Contributed by Mario Carneiro,
       24-Jul-2014.) $)
    ofc1 $p |- ( ( ph /\ X e. A ) ->
      ( ( ( A X. { B } ) oF R F ) ` X ) = ( B R C ) ) $=
      ( csn cxp wcel wf wfn fconstg ffn 3syl inidm wceq fvconst2g sylan ofval
      cfv ) ABBCDEBBCNZOZFGGIACHPZBUHUIQUIBRKBCHSBUHUITUALJJBUBAUJIBPIUIUGCUCKB
      CIHUDUEMUF $.
      $( [24-Jul-2014] $)
  $}

  ${
    ofc2.1 $e |- ( ph -> A e. V ) $.
    ofc2.2 $e |- ( ph -> B e. W ) $.
    ofc2.3 $e |- ( ph -> F Fn A ) $.
    ofc2.4 $e |- ( ( ph /\ X e. A ) -> ( F ` X ) = C ) $.
    $( Right operation by a constant. $)
    ofc2 $p |- ( ( ph /\ X e. A ) ->
      ( ( F oF R ( A X. { B } ) ) ` X ) = ( C R B ) ) $=
      ( csn cxp wcel wf wfn fconstg ffn 3syl inidm wceq fvconst2g sylan ofval
      cfv ) ABBDCEBFBCNZOZGGILACHPZBUHUIQUIBRKBCHSBUHUITUAJJBUBMAUJIBPIUIUGCUCK
      BCIHUDUEUF $.
      $( [7-Oct-2014] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x ph $.  $d x R $.  $d x W $.
    $d x X $.
    ofc12.1 $e |- ( ph -> A e. V ) $.
    ofc12.2 $e |- ( ph -> B e. W ) $.
    ofc12.3 $e |- ( ph -> C e. X ) $.
    $( Function operation on two constant functions.  (Contributed by Mario
       Carneiro, 28-Jul-2014.) $)
    ofc12 $p |- ( ph ->
      ( ( A X. { B } ) oF R ( A X. { C } ) ) = ( A X. { ( B R C ) } ) ) $=
      ( vx csn cxp co cmpt wcel adantr wceq fconstmpt cof a1i offval2 syl6eqr
      cv ) ABCMNZBDMNZEUAOLBCDEOZPBUHMNALBCDEUFUGFGHIACGQLUEBQZJRADHQUIKRUFLBCP
      SALBCTUBUGLBDPSALBDTUBUCLBUHTUD $.
      $( [28-Jul-2014] $)
  $}

  ${
    $d w x B $.  $d w x C $.  $d w x y z F $.  $d w x y z G $.  $d w x y z H $.
    $d w x y z O $.  $d w x y z P $.  $d w x y z ph $.  $d w x y z R $.
    $d w A $.  $d w x y z S $.  $d w x y z T $.  $d w x y z U $.
    caofref.1 $e |- ( ph -> A e. V ) $.
    caofref.2 $e |- ( ph -> F : A --> S ) $.
    ${
      caofref.3 $e |- ( ( ph /\ x e. S ) -> x R x ) $.
      $( Transfer a reflexive law to the function relation.  (Contributed by
         Mario Carneiro, 28-Jul-2014.) $)
      caofref $p |- ( ph -> F oR R F ) $=
        ( vw cofr wbr cv cfv wral wcel wa wf ralrimiva ffvelrn sylan id breq12d
        adantr wceq rcla4v sylc wfn ffn syl inidm eqidd ofrfval mpbird ) AFFDLM
        KNZFOZUQDMZKCPAURKCAUPCQZRZUQEQZBNZVBDMZBEPZURACEFSZUSVAICEUPFUAUBAVDUS
        AVCBEJTUEVCURBUQEVBUQUFZVBUQVBUQDVFUCZVGUDUGUHTAKCCUQUQDCFFGGAVEFCUIICE
        FUJUKZVHHHCULUTUQUMZVIUNUO $.
        $( [28-Jul-2014] $)
    $}

    ${
      $d v A $.  $d v F $.  $d x v N $.  $d v S $.  $d v ph $.  $d v w $.
      caofinv.3 $e |- ( ph -> B e. W ) $.
      caofinv.4 $e |- ( ph -> N : S --> S ) $.
      caofinv.5 $e |- ( ph -> G = ( v e. A |-> ( N ` ( F ` v ) ) ) ) $.
      ${
        caofinvl.6 $e |- ( ( ph /\ x e. S ) -> ( ( N ` x ) R x ) = B ) $.
        $( Transfer a left inverse law to the function operation. $)
        caofinvl $p |- ( ph -> ( G oF R F ) = ( A X. { B } ) ) $=
          ( vw cfv cof co cmpt csn cxp cv wf wcel wa ffvelrn sylan syl2anc eqid
          adantr fmptd feq1d mpbird wfn wceq wral fvex rgenw fnmpt ax-mp fneq1d
          cvv mpbiri dffn5v sylib ffn syl offval2 fveq1d weq fveq2 fveq2d fvmpt
          sylan9eq oveq1d ralrimiva id oveq12d eqeq1d eqtrd mpteq2dva fconstmpt
          rcla4va syl6eqr ) AIHFUAUBZSDEUCZDEUDUEAWISDSUFZITZWKHTZFUBZUCWJASDWL
          WMFIHKGGMADGIUGZWKDUHZWLGUHAWODGCDCUFZHTZJTZUCZUGACDWSGWTAWQDUHZUIGGJ
          UGZWRGUHZWSGUHAXBXAPUNADGHUGZXAXCNDGWQHUJUKGGWRJUJULWTUMZUOADGIWTQUPU
          QDGWKIUJUKAXDWPWMGUHZNDGWKHUJUKZAIDURZISDWLUCUSAXHWTDURZWSVFUHZCDUTXI
          XJCDWRJVAVBCDWSWTVFXEVCVDADIWTQVEVGSDIVHVIAHDURZHSDWMUCUSAXDXKNDGHVJV
          KSDHVHVIVLASDWNEAWPUIZWNWMJTZWMFUBZEXLWLXMWMFAWPWLWKWTTXMAWKIWTQVMCWK
          WSXMDWTCSVNWRWMJWQWKHVOVPXEWMJVAVQVRVSXLXFBUFZJTZXOFUBZEUSZBGUTZXNEUS
          ZXGAXSWPAXRBGRVTUNXRXTBWMGXOWMUSZXQXNEYAXPXMXOWMFXOWMJVOYAWAWBWCWGULW
          DWEWDSDEWFWH $.
          $( [22-Oct-2014] $)
      $}
    $}

    ${
      caofid0.3 $e |- ( ph -> B e. W ) $.
      ${
        caofid0l.5 $e |- ( ( ph /\ x e. S ) -> ( B R x ) = x ) $.
        $( Transfer a left identity law to the function operation. $)
        caofid0l $p |- ( ph -> ( ( A X. { B } ) oF R F ) = F ) $=
          ( vw cv cfv wcel wfn wceq sylan csn cxp fnconstg syl wf ffn fvconst2g
          wa eqidd co ffvelrn ralrimiva oveq2 id eqeq12d rcla4cva syldan offveq
          wral ) ANCDNOZGPZECDUAUBZGGHJADIQZVBCRLCDIUCUDACFGUEZGCRKCFGUFUDZVEAV
          CUTCQZUTVBPDSLCDUTIUGTAVFUHVAUIAVFVAFQZDVAEUJZVASZAVDVFVGKCFUTGUKTADB
          OZEUJZVJSZBFUSVGVIAVLBFMULVLVIBVAFVJVASZVKVHVJVAVJVADEUMVMUNUOUPTUQUR
          $.
          $( [21-Oct-2014] $)
      $}

      ${
        caofid0r.5 $e |- ( ( ph /\ x e. S ) -> ( x R B ) = x ) $.
        $( Transfer a right identity law to the function operation. $)
        caofid0r $p |- ( ph -> ( F oF R ( A X. { B } ) ) = F ) $=
          ( vw cv cfv wfn wcel wceq sylan csn cxp wf ffn syl fnconstg fvconst2g
          wa eqidd co ffvelrn ralrimiva oveq1 id eqeq12d rcla4cva syldan offveq
          wral ) ANCNOZGPZDEGCDUAUBZGHJACFGUCZGCQKCFGUDUEZADIRZVBCQLCDIUFUEVDAU
          TCRZUHVAUIAVEVFUTVBPDSLCDUTIUGTAVFVAFRZVADEUJZVASZAVCVFVGKCFUTGUKTABO
          ZDEUJZVJSZBFUSVGVIAVLBFMULVLVIBVAFVJVASZVKVHVJVAVJVADEUMVMUNUOUPTUQUR
          $.
          $( [21-Oct-2014] $)
      $}

      caofid1.4 $e |- ( ph -> C e. X ) $.
      ${
        caofid1.5 $e |- ( ( ph /\ x e. S ) -> ( x R B ) = C ) $.
        $( Transfer a right absorption law to the function operation.
           (Contributed by Mario Carneiro, 28-Jul-2014.) $)
        caofid1 $p |- ( ph -> ( F oF R ( A X. { B } ) ) = ( A X. { C } ) ) $=
          ( cfv wcel wceq sylan vw cv csn cxp wf wfn ffn syl fnconstg fvconst2g
          wa eqidd ffvelrn ralrimiva oveq1 eqeq1d rcla4cva syldan eqtr4d offveq
          co wral ) AUACUAUBZHQZDFHCDUCUDZCEUCUDZILACGHUEZHCUFMCGHUGUHADJRZVECU
          FNCDJUIUHAEKRZVFCUFOCEKUIUHAVCCRZUKZVDULAVHVJVCVEQDSNCDVCJUJTVKVDDFVA
          ZEVCVFQZAVJVDGRZVLESZAVGVJVNMCGVCHUMTABUBZDFVAZESZBGVBVNVOAVRBGPUNVRV
          OBVDGVPVDSVQVLEVPVDDFUOUPUQTURAVIVJVMESOCEVCKUJTUSUT $.
          $( [28-Jul-2014] $)
      $}

      caofid2.5 $e |- ( ( ph /\ x e. S ) -> ( B R x ) = C ) $.
      $( Transfer a right absorption law to the function operation.
         (Contributed by Mario Carneiro, 28-Jul-2014.) $)
      caofid2 $p |- ( ph -> ( ( A X. { B } ) oF R F ) = ( A X. { C } ) ) $=
        ( cfv wcel wceq sylan vw cv csn cxp wfn fnconstg syl wf fvconst2g eqidd
        wa co ffvelrn wral ralrimiva oveq2 eqeq1d rcla4cva syldan eqtr4d offveq
        ffn ) AUACDUAUBZHQZFCDUCUDZHCEUCUDZILADJRZVECUENCDJUFUGACGHUHZHCUEMCGHV
        BUGAEKRZVFCUEOCEKUFUGAVGVCCRZVCVEQDSNCDVCJUITAVJUKZVDUJVKDVDFULZEVCVFQZ
        AVJVDGRZVLESZAVHVJVNMCGVCHUMTADBUBZFULZESZBGUNVNVOAVRBGPUOVRVOBVDGVPVDS
        VQVLEVPVDDFUPUQURTUSAVIVJVMESOCEVCKUITUTVA $.
        $( [28-Jul-2014] $)
    $}

    caofcom.3 $e |- ( ph -> G : A --> S ) $.
    ${
      caofcom.4 $e |- ( ( ph /\ ( x e. S /\ y e. S ) ) ->
                        ( x R y ) = ( y R x ) ) $.
      $( Transfer a commutative law to the function operation.  (Contributed by
         Mario Carneiro, 26-Jul-2014.) $)
      caofcom $p |- ( ph -> ( F oF R G ) = ( G oF R F ) ) $=
        ( vw cfv co cmpt wcel wa wceq cv cof ffvelrn sylan jca syldan mpteq2dva
        wf caovcomg wfn ffn syl dffn5v sylib offval2 3eqtr4d ) ANDNUAZGOZUQHOZE
        PZQNDUSUREPZQGHEUBZPHGVBPANDUTVAAUQDRZURFRZUSFRZSUTVATAVCSVDVEADFGUHZVC
        VDKDFUQGUCUDZADFHUHZVCVELDFUQHUCUDZUEABCURUSFEMUIUFUGANDURUSEGHIFFJVGVI
        AGDUJZGNDURQTAVFVJKDFGUKULNDGUMUNZAHDUJZHNDUSQTAVHVLLDFHUKULNDHUMUNZUOA
        NDUSUREHGIFFJVIVGVMVKUOUP $.
        $( [26-Jul-2014] $)
    $}

    ${
      caofrss.4 $e |- ( ( ph /\ ( x e. S /\ y e. S ) ) ->
                        ( x R y -> x T y ) ) $.
      $( Transfer a relation subset law to the function relation.  (Contributed
         by Mario Carneiro, 28-Jul-2014.) $)
      caofrss $p |- ( ph -> ( F oR R G -> F oR T G ) ) $=
        ( vw cv wbr wral wcel wi cfv cofr wa wf ffvelrn sylan ralrimivva adantr
        wceq breq1 imbi12d breq2 rcla42va syl21anc ralimdva wfn ffn inidm eqidd
        syl ofrfval 3imtr4d ) AOPZHUAZVCIUAZEQZODRVDVEGQZODRHIEUBQHIGUBQAVFVGOD
        AVCDSZUCZVDFSZVEFSZBPZCPZEQZVLVMGQZTZCFRBFRZVFVGTZADFHUDZVHVJLDFVCHUEUF
        ADFIUDZVHVKMDFVCIUEUFAVQVHAVPBCFFNUGUHVPVRVDVMEQZVDVMGQZTBCVDVEFFVLVDUI
        VNWAVOWBVLVDVMEUJVLVDVMGUJUKVMVEUIWAVFWBVGVMVEVDEULVMVEVDGULUKUMUNUOAOD
        DVDVEEDHIJJAVSHDUPLDFHUQUTZAVTIDUPMDFIUQUTZKKDURZVIVDUSZVIVEUSZVAAODDVD
        VEGDHIJJWCWDKKWEWFWGVAVB $.
        $( [28-Jul-2014] $)
    $}

    caofass.4 $e |- ( ph -> H : A --> S ) $.
    ${
      caofass.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                        ( ( x R y ) T z ) = ( x O ( y P z ) ) ) $.
      $( Transfer an associative law to the function operation.  (Contributed
         by Mario Carneiro, 26-Jul-2014.) $)
      caofass $p |- ( ph ->
        ( ( F oF R G ) oF T H ) = ( F oF O ( G oF P H ) ) ) $=
        ( co vw cv cfv cmpt cof wcel wa wceq wral ralrimivvva adantr wi ffvelrn
        wf sylan oveq1 oveq1d eqeq12d oveq2d rcla43v syl3anc mpd mpteq2dva ovex
        oveq2 cvv a1i wfn ffn syl dffn5v sylib offval2 3eqtr4d ) AUAEUAUBZJUCZV
        OKUCZGTZVOLUCZITZUDUAEVPVQVSFTZMTZUDJKGUETZLIUETJKLFUETZMUETAUAEVTWBAVO
        EUFZUGZBUBZCUBZGTZDUBZITZWGWHWJFTZMTZUHZDHUICHUIBHUIZVTWBUHZAWOWEAWNBCD
        HHHSUJUKWFVPHUFZVQHUFZVSHUFZWOWPULAEHJUNZWEWQPEHVOJUMUOZAEHKUNZWEWRQEHV
        OKUMUOZAEHLUNZWEWSREHVOLUMUOZWNWPVPWHGTZWJITZVPWLMTZUHVRWJITZVPVQWJFTZM
        TZUHBCDVPVQVSHHHWGVPUHZWKXGWMXHXLWIXFWJIWGVPWHGUPUQWGVPWLMUPURWHVQUHZXG
        XIXHXKXMXFVRWJIWHVQVPGVEUQXMWLXJVPMWHVQWJFUPUSURWJVSUHZXIVTXKWBWJVSVRIV
        EXNXJWAVPMWJVSVQFVEUSURUTVAVBVCAUAEVRVSIWCLNVFHOVRVFUFWFVPVQGVDVGXEAUAE
        VPVQGJKNHHOXAXCAJEVHZJUAEVPUDUHAWTXOPEHJVIVJUAEJVKVLZAKEVHZKUAEVQUDUHAX
        BXQQEHKVIVJUAEKVKVLZVMALEVHZLUAEVSUDUHAXDXSREHLVIVJUAELVKVLZVMAUAEVPWAM
        JWDNHVFOXAWAVFUFWFVQVSFVDVGXPAUAEVQVSFKLNHHOXCXEXRXTVMVMVN $.
        $( [26-Jul-2014] $)
    $}

    ${
      caoftrn.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                        ( ( x R y /\ y T z ) -> x U z ) ) $.
      $( Transfer a transitivity law to the function relation.  (Contributed by
         Mario Carneiro, 28-Jul-2014.) $)
      caoftrn $p |- ( ph -> ( ( F oR R G /\ G oR T H ) -> F oR U H ) ) $=
        ( vw wbr cv cfv wa wral cofr wcel wi ralrimivvva adantr wf ffvelrn wceq
        sylan breq1 anbi1d imbi12d breq2 anbi12d imbi1d anbi2d rcla43v ralimdva
        syl3anc mpd wfn ffn syl inidm eqidd ofrfval r19.26 syl6bbr 3imtr4d ) AS
        UAZJUBZVNKUBZFTZVPVNLUBZHTZUCZSEUDZVOVRITZSEUDJKFUETZKLHUETZUCZJLIUETAV
        TWBSEAVNEUFZUCZBUAZCUAZFTZWIDUAZHTZUCZWHWKITZUGZDGUDCGUDBGUDZVTWBUGZAWP
        WFAWOBCDGGGRUHUIWGVOGUFZVPGUFZVRGUFZWPWQUGAEGJUJZWFWROEGVNJUKUMAEGKUJZW
        FWSPEGVNKUKUMAEGLUJZWFWTQEGVNLUKUMWOWQVOWIFTZWLUCZVOWKITZUGVQVPWKHTZUCZ
        XFUGBCDVOVPVRGGGWHVOULZWMXEWNXFXIWJXDWLWHVOWIFUNUOWHVOWKIUNUPWIVPULZXEX
        HXFXJXDVQWLXGWIVPVOFUQWIVPWKHUNURUSWKVRULZXHVTXFWBXKXGVSVQWKVRVPHUQUTWK
        VRVOIUQUPVAVCVDVBAWEVQSEUDZVSSEUDZUCWAAWCXLWDXMASEEVOVPFEJKMMAXAJEVEOEG
        JVFVGZAXBKEVEPEGKVFVGZNNEVHZWGVOVIZWGVPVIZVJASEEVPVRHEKLMMXOAXCLEVEQEGL
        VFVGZNNXPXRWGVRVIZVJURVQVSSEVKVLASEEVOVRIEJLMMXNXSNNXPXQXTVJVM $.
        $( [28-Jul-2014] $)
    $}

    ${
      caofdi.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                          ( x T ( y R z ) ) = ( ( x T y ) R ( x T z ) ) ) $.
      $( Transfer a distributive law to the function operation.  (Contributed
         by Mario Carneiro, 26-Jul-2014.) $)
      caofdi $p |- ( ph ->
        ( F oF T ( G oF R H ) ) = ( ( F oF T G ) oF R ( F oF T H ) ) ) $=
        ( vw co wcel cv cfv cmpt cof w3a wceq wa wf ffvelrn 3jca caovdig syldan
        sylan mpteq2dva cvv ovex a1i wfn ffn syl dffn5v sylib offval2 3eqtr4d )
        ARERUAZIUBZVEJUBZVEKUBZFSZHSZUCREVFVGHSZVFVHHSZFSZUCIJKFUDZSZHUDZSIJVPS
        ZIKVPSZVNSAREVJVMAVEETZVFGTZVGGTZVHGTZUEVJVMUFAVSUGZVTWAWBAEGIUHZVSVTNE
        GVEIUIUMZAEGJUHZVSWAOEGVEJUIUMZAEGKUHZVSWBPEGVEKUIUMZUJABCDVFVGVHGFHQUK
        ULUNAREVFVIHIVOLGUOMWEVIUOTWCVGVHFUPUQAIEURZIREVFUCUFAWDWJNEGIUSUTREIVA
        VBZAREVGVHFJKLGGMWGWIAJEURZJREVGUCUFAWFWLOEGJUSUTREJVAVBZAKEURZKREVHUCU
        FAWHWNPEGKUSUTREKVAVBZVCVCAREVKVLFVQVRLUOUOMVKUOTWCVFVGHUPUQVLUOTWCVFVH
        HUPUQAREVFVGHIJLGGMWEWGWKWMVCAREVFVHHIKLGGMWEWIWKWOVCVCVD $.
        $( [26-Jul-2014] $)
    $}

    ${
      caofdir.5 $e |- ( ( ph /\ ( x e. S /\ y e. S /\ z e. S ) ) ->
                          ( ( x R y ) T z ) = ( ( x T z ) R ( y T z ) ) ) $.
      $( Transfer a reverse distributive law to the function operation. $)
      caofdir $p |- ( ph ->
        ( ( F oF R G ) oF T H ) = ( ( F oF T H ) oF R ( G oF T H ) ) ) $=
        ( vw co wcel cv cfv cmpt cof w3a wceq wa wf ffvelrn sylan 3jca caovdirg
        syldan mpteq2dva cvv ovex a1i wfn ffn syl dffn5v sylib offval2 3eqtr4d
        ) ARERUAZIUBZVEJUBZFSZVEKUBZHSZUCREVFVIHSZVGVIHSZFSZUCIJFUDZSZKHUDZSIKV
        PSZJKVPSZVNSAREVJVMAVEETZVFGTZVGGTZVIGTZUEVJVMUFAVSUGZVTWAWBAEGIUHZVSVT
        NEGVEIUIUJZAEGJUHZVSWAOEGVEJUIUJZAEGKUHZVSWBPEGVEKUIUJZUKABCDVFVGVIGFHQ
        ULUMUNAREVHVIHVOKLUOGMVHUOTWCVFVGFUPUQWIAREVFVGFIJLGGMWEWGAIEURZIREVFUC
        UFAWDWJNEGIUSUTREIVAVBZAJEURZJREVGUCUFAWFWLOEGJUSUTREJVAVBZVCAKEURZKREV
        IUCUFAWHWNPEGKUSUTREKVAVBZVCAREVKVLFVQVRLUOUOMVKUOTWCVFVIHUPUQVLUOTWCVG
        VIHUPUQAREVFVIHIKLGGMWEWIWKWOVCAREVGVIHJKLGGMWGWIWMWOVCVCVD $.
        $( [19-Oct-2014] $)
    $}
  $}

  ${
    $d f g A $.  $d f g B $.  $d f g x R $.
    $( Equivalent expressions for a restriction of the function operation map.
       Unlike ` oF R ` which is a proper class, ` ( oF R | `` ( A X. B ) ) `
       can be a set by ~ ofmresex , allowing it to be used as a function or
       structure argument.  By ~ ofmresval , the restricted operation map
       values are the same as the original values, allowing theorems for
       ` oF R ` to be reused. $)
    ofmres $p |- ( oF R |` ( A X. B ) ) =
         ( f e. A , g e. B |-> ( f oF R g ) ) $=
      ( vx cof cres cv cdm cfv co cmpt2 cvv wss wceq ssv eqid wcel vex cxp cmpt
      cin df-of reseq1i resmpt2 mp2an eqtri dmex inex1 mptex ovmpt4g mpt2eq123i
      mp3an eqtr4i ) CGZABUAZHZDEABFDIZJZEIZJZUCZFIZUSKVDVAKCLZUBZMZDEABUSVAUPL
      ZMURDENNVFMZUQHZVGUPVIUQFCDEUDZUEANOBNOVJVGPAQBQDENNABVFUFUGUHDEABVHABVFA
      RBRUSNSVANSVFNSVHVFPDTZETFVCVEUTVBUSVLUIUJUKDENNVFUPNVKULUNUMUO $.
      $( [20-Oct-2014] $)

    ofmresval.f $e |- ( ph -> F e. A ) $.
    ofmresval.g $e |- ( ph -> G e. B ) $.
    $( Value of a restriction of the function operation map. $)
    ofmresval $p |- ( ph -> ( F ( oF R |` ( A X. B ) ) G ) = ( F oF R G ) ) $=
      ( wcel cof cxp cres co wceq ovres syl2anc ) AEBIFCIEFDJZBCKLMEFQMNGHEFBCQ
      OP $.
      $( [20-Oct-2014] $)
  $}

  ${
    $d f g A $.  $d f g B $.  $d f g R $.
    ofmresex.a $e |- ( ph -> A e. V ) $.
    ofmresex.b $e |- ( ph -> B e. W ) $.
    $( Existence of a restriction of the function operation map. $)
    ofmresex $p |- ( ph -> ( oF R |` ( A X. B ) ) e. _V ) $=
      ( cxp cvv wcel cof cres xpexg syl2anc ofexg syl ) ABCIZJKZDLRMJKABEKCFKSG
      HBCEFNORDJPQ $.
      $( [20-Oct-2014] $)
  $}

