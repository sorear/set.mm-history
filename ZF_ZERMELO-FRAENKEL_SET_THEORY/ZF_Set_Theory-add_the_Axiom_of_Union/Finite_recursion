$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
              Finite recursion
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( We use the next 3 theorems for finite recursion.  Note that we take
     advantage of what we have done for transfinite recursion, which allows
     us to explicitly specify the function and moreover not require the
     existence of omega. $)

  $( The function generated by finite recursive definition generation is a
     function on omega. $)
  frfnom $p |- ( rec ( F , A ) |` om ) Fn om $=
    ( crdg com cres wfn wfun cdm wceq df-fn con0 rdgfnon fnfun ax-mp funres cin
    fndm ineq2i dmres wss omsson dfss mpbi 3eqtr4i mpbir2an ) BACZDEZDFUGGZUGHZ
    DIUGDJUFGZUHUFKFZUJABLZKUFMNDUFONDUFHZPDKPZUIDUMKDUKUMKIULKUFQNRUFDSDKTDUNI
    UADKUBUCUDUE $.
    $( [15-Oct-1996] $)

  $( The initial value resulting from finite recursive definition
     generation. $)
  fr0g $p |- ( A e. B -> ( ( rec ( F , A ) |` om ) ` (/) ) = A ) $=
    ( wcel c0 crdg com cres cfv wceq peano1 fvres ax-mp rdg0g syl5eq ) ABDECAFZ
    GHIZEPIZAEGDQRJKEGPLMABCNO $.
    $( [15-Oct-1996] $)

  $( The successor value resulting from finite recursive definition
     generation. $)
  frsuc $p |- ( B e. om -> ( ( rec ( F , A ) |` om ) ` suc B ) =
              ( F ` ( ( rec ( F , A ) |` om ) ` B ) ) ) $=
    ( com wcel csuc crdg cfv cres con0 wceq nnon rdgsuc syl peano2b fvres sylbi
    fveq2d 3eqtr4d ) BDEZBFZCAGZHZBUBHZCHZUAUBDIZHZBUFHZCHTBJEUCUEKBLABCMNTUADE
    UGUCKBOUADUBPQTUHUDCBDUBPRS $.
    $( [15-Oct-1996] $)

  ${
    $d z D $.  $d y z C $.  $d z A $.  $d z B $.  $d z F $.  $d x y z $.
    frsucopab.1 $e |- ( z e. A -> A. x z e. A ) $.
    frsucopab.2 $e |- ( z e. B -> A. x z e. B ) $.
    frsucopab.3 $e |- ( z e. D -> A. x z e. D ) $.
    frsucopab.4 $e |- F = ( rec ( { <. x , y >. | y = C } , A ) |` om ) $.
    frsucopab.5 $e |- ( x = ( F ` B ) -> C = D ) $.
    $( The successor value resulting from finite recursive definition
       generation (special case where the generation function is an ordered
       pair abstraction). $)
    frsucopab $p |- ( ( B e. om /\ D e. R ) -> ( F ` suc B ) = D ) $=
      ( com wcel cfv cv wceq fveq1i csuc copab crdg cres frsuc 3eqtr4g cvv fvex
      fveq2i hbopab1 hbrdg ax-17 hbres hbxfreq hbfv fvopabgf mpan sylan9eq ) EO
      PZGHPZEUAZIQZEIQZBRFSZABUBZQZGUSVAVEDUCZOUDZQEVHQZVEQVBVFDEVEUEVAIVHMTVCV
      IVEEIVHMTUIUFVCUGPUTVFGSEIUHABCVCFGUGHACEIACIVHMACVGOACDVEVDABCUJJUKCROPA
      ULUMUNKUOLNUPUQUR $.
      $( [14-Sep-2003] $)
  $}

  ${
    $d z D $.  $d y z C $.  $d z A $.  $d z B $.  $d z F $.  $d x y z $.
    frsucmpt.1 $e |- ( z e. A -> A. x z e. A ) $.
    frsucmpt.2 $e |- ( z e. B -> A. x z e. B ) $.
    frsucmpt.3 $e |- ( z e. D -> A. x z e. D ) $.
    frsucmpt.4 $e |- F = ( rec ( ( x e. _V |-> C ) , A ) |` om ) $.
    frsucmpt.5 $e |- ( x = ( F ` B ) -> C = D ) $.
    $( The successor value resulting from finite recursive definition
       generation (special case where the generation function is expressed in
       maps-to notation). $)
    frsucmpt $p |- ( ( B e. om /\ D e. R ) -> ( F ` suc B ) = D ) $=
      ( vy cvv crdg com cres cv wceq cmpt copab wcel wa df-mpt biantrur opabbii
      vex eqtr4i rdgeq1 ax-mp reseq1 eqtri frsucopab ) ANBCDEFGHIJKHAOEUAZCPZQR
      ZNSETZANUBZCPZQRZLUPUTTZUQVATUOUSTVBUOASOUCZURUDZANUBUSANOEUEURVDANVCURAU
      HUFUGUICUOUSUJUKUPUTQULUKUMMUN $.
      $( [2-Nov-2011] $)
  $}

  ${
    $d y z w A $.  $d x y z w F $.
    tz7.48.1 $e |- F Fn On $.
    ${
      $d x A $.
      $( A way of showing an ordinal function is one-to-one. $)
      tz7.48lem $p |- ( ( A C_ On /\ A. x e. A A. y e. x
                    -. ( F ` x ) = ( F ` y ) ) -> Fun `' ( F |` A ) ) $=
        ( vw vz con0 cv cfv wceq wn wral wa wi wcel wal r2al fveq2 notbid simpl
        cres ccnv wfun anim1i imim1i exp3a 2alimi 3imtr4i eleq1 imbi12d cbvralv
        wss eqeq2d ralbii eleq2 eqeq1d ralbidv bitri 3bitri ralcom2 sylbi ancri
        r19.26-2 sylibr syl wb fvres eqeqan12d ad2antrl anim12d wo pm3.48 oridm
        ssel eqcom notbii orbi1i bitr3i syl6ibr con2d word eloni ordtri3 syl2an
        biimprd syl9r syl6 imp32 sylbid exp32 a2d alimdv 3imtr4g syl5 imdistani
        wfn fnssres mpan cvv wf dffn2 wf1 dff13 df-f1 simprbi sylanb sylan ) CH
        UMZAIZDJZBIZDJZKZLZBXJMACMZNXIXJDCUBZJZXLXQJZKZXJXLKZOZBCMACMZNXQUCUDZX
        IXPYCXPXJXLPZXMXKKZLZOZXLXJPZXOOZNZBCMACMZXIYCXPYJBCMZACMZYLXJCPZYINZXO
        OZBQAQYOXLCPZNZYJOZBQAQXPYNYQYTABYQYSYIXOYSYINYPXOYSYOYIYOYRUAUEUFUGUHX
        OABCXJRYJABCCRUIYNYHBCMACMZYNNYLYNUUAYNYHACMZBCMZUUAYNFIZXJPZXKUUDDJZKZ
        LZOZFCMZACMUUDGIZPZUUKDJZUUFKZLZOZFCMZGCMZUUCYMUUJACYJUUIBFCXLUUDKZYIUU
        EXOUUHXLUUDXJUJUUSXNUUGUUSXMUUFXKXLUUDDSUNTUKULUOUUJUUQAGCXJUUKKZUUIUUP
        FCUUTUUEUULUUHUUOXJUUKUUDUPUUTUUGUUNUUTXKUUMUUFXJUUKDSUQTUKURULUURXJUUK
        PZUUMXKKZLZOZACMZGCMUUCUUQUVEGCUUPUVDFACUUDXJKZUULUVAUUOUVCUUDXJUUKUJUV
        FUUNUVBUVFUUFXKUUMUUDXJDSUNTUKULUOUVEUUBGBCUUKXLKZUVDYHACUVGUVAYEUVCYGU
        UKXLXJUPUVGUVBYFUVGUUMXMXKUUKXLDSUQTUKURULUSUTYHBACVAVBVCYHYJABCCVDVEVF
        XIYSYKOZBQZAQYSYBOZBQZAQYLYCXIUVIUVKAXIUVHUVJBXIYSYKYBXIYSYKYBXIYSYKNNX
        TXNYAYSXTXNVGXIYKYOYRXRXKXSXMXJCDVHXLCDVHVIVJXIYSYKXNYAOZXIYSXJHPZXLHPZ
        NZYKUVLOXIYOUVMYRUVNCHXJVOCHXLVOVKYKXNYEYIVLZLZUVOYAYKUVPXNYKUVPYGXOVLZ
        XOYEYGYIXOVMXOXOXOVLUVRXOVNXOYGXOXNYFXKXMVPVQVRVSVTWAUVMXJWBZXLWBZUVQYA
        OUVNXJWCXLWCUVSUVTNYAUVQXJXLWDWFWEWGWHWIWJWKWLWMWMYKABCCRYBABCCRWNWOWPX
        IXQCWQZYCYDDHWQXIUWAEHCDWRWSUWACWTXQXAZYCYDCXQXBUWBYCNZUWBYDUWCCWTXQXCU
        WBYDNABCWTXQXDCWTXQXEVSXFXGXHVF $.
        $( [9-Feb-1997] $)
    $}

    $( Proposition 7.48(2) of [TakeutiZaring] p. 51.  (The proof was shortened
       by Mario Carneiro, 10-Jan-2013.)  (Unnecessary distinct variable
       restrictions were removed by David Abernethy, 5-May-2013.) $)
    tz7.48-2 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) -> Fun `' F ) $=
      ( vy cv cfv cima cdif wcel con0 wral cres ccnv wfun wss wceq wa wi ax-mp
      wn ssid wel onelon ancoms cdm wfn fndm eleq2i funfvima mpan impcom eleq1a
      fnfun eldifn nsyli syl sylan2br expimpd com12 ralrimiv ralimiaa tz7.48lem
      syldan sylancr wrel fnrel eqimssi relssres mp2an cnveqi funeqi sylib ) AF
      ZCGZBCVNHZIJZAKLZCKMZNZOZCNZOVRKKPVOEFZCGZQZUAZEVNLZAKLWAKUBVQWGAKVNKJZVQ
      RZWFEVNEAUCZWIWFWJWHVQWFWJWHWCKJZVQWFSZWHWJWKVNWCUDUEWKWJWCCUFZJZWLWMKWCC
      KUGZWMKQDKCUHTZUIWJWNRWDVPJZWLWNWJWQCOZWNWJWQSWOWRDKCUNTVNWCCUJUKULWQWEVO
      VPJVQWDVPVOUMVOBVPUOUPUQURVDUSUTVAVBAEKCDVCVEVTWBVSCCVFZWMKPVSCQWOWSDKCVG
      TWMKWPVHCKVIVJVKVLVM $.
      $( [5-May-2013] $) $( [9-Feb-1997] $)

    $d x A $.
    $( Proposition 7.48(1) of [TakeutiZaring] p. 51. $)
    tz7.48-1 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) ->
                   ran F C_ A ) $=
      ( vy cv cfv cima cdif wcel con0 wral crn wceq wa wex cop vex elrn2 wi cdm
      opeldm wfn fndm ax-mp syl6eleq ancri wb fnopfvb mpan pm5.32i sylibr eximi
      sylbi hbra1 ax-17 ra4 eldifi eleq1 syl5ibcom imim2i imp3a syl exlimd syl5
      ssrdv ) AFZCGZBCVGHZIJZAKLZECMZBEFZVLJZVGKJZVHVMNZOZAPZVKVMBJZVNVGVMQCJZA
      PVRAVMCERZSVTVQAVTVOVTOVQVTVOVTVGCUAZKVGVMCARUBCKUCZWBKNDKCUDUEUFUGVOVPVT
      WCVOVPVTUHDKVGVMCWAUIUJUKULUMUNVKVQVSAVJAKUOVSAUPVKVOVJTZVQVSTVJAKUQWDVOV
      PVSVJVPVSTVOVJVHBJVPVSVHBVIURVHVMBUSUTVAVBVCVDVEVF $.
      $( [9-Feb-1997] $)

    $( Proposition 7.48(3) of [TakeutiZaring] p. 51. $)
    tz7.48-3 $p |- ( A. x e. On ( F ` x ) e. ( A \ ( F " x ) ) ->
                    -. A e. _V ) $=
      ( cv cfv cima cdif wcel con0 wral cvv crn cdm onprc wfn wceq eleq1i syl
      wi fndm ax-mp mtbir ccnv wfun tz7.48-2 funrnex com12 df-rn dfdm4 mtoi wss
      3imtr4g tz7.48-1 ssexg ex mtod ) AEZCFBCURGHIAJKZBLIZCMZLIZUSVBCNZLIZVDJL
      IOVCJLCJPVCJQDJCUAUBRUCUSCUDZUEZVBVDTABCDUFVFVENZLIZVEMZLIZVBVDVHVFVJLVEU
      GUHVAVGLCUIRVCVILCUJRUMSUKUSVABULZUTVBTABCDUNVKUTVBVABLUOUPSUQ $.
      $( [9-Feb-1997] $)

    ${
      tz7.49OLD.2 $e |- A e. _V $.
      $( Proposition 7.49 of [TakeutiZaring] p. 51. $)
      tz7.49OLD $p |- ( A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                   ( F ` x ) e. ( A \ ( F " x ) ) ) ->
                   E. x e. On ( A. y e. x ( A \ ( F " y ) ) =/= (/) /\
                   ( F " x ) = A /\ Fun `' ( F |` x ) ) ) $=
        ( vz cv c0 cfv wcel wi con0 wral wceq wa wrex wn wss com23 cima wne w3a
        cdif cres ccnv wfun cvv tz7.48-3 df-ne ralbii ralim syl5bir mtoi dfrex2
        mt2 sylibr imaeq2 difeq2d eqeq1d onminex syl anbi2i hbra1 simpllr fnfun
        rexbii wfn ax-mp fvelima mpan ax-17 adantld onelon neeq1d fveq2 eleq12d
        hban ra4 imbi12d rcla4v sylcom com3r exp3a eldifi eleq1 syl5ibcom com34
        imp syl8 rexlimd syl5 ssrdv biimpri anim12i eqss onss wal ssel cdm fndm
        ssdif0 syl6sseqr funfvima2 sylancr syl6 com12 syldd imp4a eldifn eleq1a
        con3d syl5com com4r alrimdv alrimd syl6ibr ancld tz7.48lem syl56 ancoms
        a1i r2al adantr 3jca exp41 reximdai mpd ) CDAHZUAZUDZIUBZYIDJZYKKZLZAMN
        ZYKIOZCDBHZUAZUDZIUBZBYINZPZAMQZUUBYJCOZDYIUEUFUGZUCZAMQYPYQYTIOZRZBYIN
        ZPZAMQZUUDYPYQAMQZUULYPYQRZAMNZRUUMYPUUOYNAMNZUUPCUHKFACDEUIUPUUOYLAMNY
        PUUPYLUUNAMYKIUJUKYLYNAMULUMUNYQAMUOUQYQUUHABYIYROZYKYTIUUQYJYSCYIYRDUR
        USZUTVAVBUUCUUKAMUUBUUJYQUUAUUIBYIYTIUJUKVCVGUQYPUUCUUGAMYOAMVDYPYIMKZY
        QUUBUUGYPUUSUUBYQUUGYPUUBUUSYQUUGLYPUUBUUSYQUUGYPUUBPZUUSPZYQPZUUBUUEUU
        FYPUUBUUSYQVEUVBYJCSZCYJSZPUUEUVAUVCYQUVDUVAGYJCUUTUUSGHZYJKZUVECKZLUUT
        UVFUUSUVGUVFYRDJZUVEOZBYIQZUUTUUSUVGLZDUGZUVFUVJDMVHZUVLEMDVFVIZBUVEYID
        VJVKUUTUVIUVKBYIYPUUBBYPBVLZUUABYIVDZVRUVKBVLUUTYRYIKZUUSUVIUVGUUTUVQUU
        SUVHYTKZUVIUVGLUUTUUSUVQUVRUUTUUSUVQUVRYPUUBUUSUVQPZUVRLUUBUVSYPUVRUUBU
        VSUUAYPUVRLZUUBUVQUUAUUSUUABYIVSZVMUVSYRMKZUUAUVTLZYIYRVNUWBYPUUAUVRYOU
        UAUVRLAYRMUUQYLUUAYNUVRUUQYKYTIUURVOUUQYMUVHYKYTYIYRDVPUURVQVTWATZVBWBW
        CWIWDTUVRUVHCKUVIUVGUVHCYSWEUVHUVECWFWGWJWHWKWLTWIWMUVDYQCYJXBWNWOYJCWP
        UQUVAUUFYQUUTUUSUUFUUBYPUUSUUFLUUSYIMSZUUBYPPZUWEUVHUVEDJZOZRZGYRNBYINZ
        PUUFYIWQUWFUWEUWJUWFUWEUVQUVEYRKZPUWILZGWRZBWRUWJUWFUWEUWMBUUBYPBUVPUVO
        VRUWEBVLUWFUWEUWLGUWFUWEUVQUWKUWIUWEUVQUWKUWFUWIUWEUVQUWKUWGYSKZUWFUWIL
        UWEUVQUWBUWKUWNLZYIMYRWSZUWBUVLYRDWTZSUWOUVNUWBYRMUWQYRWQUVMUWQMOEMDXAV
        IXCYRUVEDXDXEXFUWEUVQUWFUWNUWIUWEUVQUWFUVRUWNUWILUWEUVQUUBYPUVRUWEUVQUU
        BUUAUVTUVQUUBUUALLUWEUUBUVQUUAUWAXGYBUWEUVQUWBUWCUWPUWDXFXHXIUVRUVHYSKZ
        RUWNUWIUVHCYSXJUWNUWHUWRUWGYSUVHXKXLXMWJWHXHXNXIXOXPUWIBGYIYRYCXQXRBGYI
        DEXSXTYAWIYDYEYFTWHXIYGYH $.
        $( [4-May-2007] $) $( [10-Feb-1997] $)

      $( Corollary of Proposition 7.49 of [TakeutiZaring] p. 51. $)
      tz7.49cOLD $p |- ( A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                   ( F ` x ) e. ( A \ ( F " x ) ) ) ->
                   E. x e. On ( F |` x ) : x -1-1-onto-> A ) $=
        ( vy cv cima cdif c0 wne cfv wcel con0 wral wceq w3a wrex wa wfn 3simpc
        wi cres ccnv wfun wf1o tz7.49OLD crn onss fnssres sylancr df-ima eqeq1i
        wss biimpi anim12i anim1i dff1o2 df-3an an32 3bitri sylibr expl reximia
        syl5 syl ) BCAGZHZIZJKVGCLVIMUBANOBCFGHIJKFVGOZVHBPZCVGUCZUDUEZQZANRVGB
        VLUFZANRAFBCDEUGVNVOANVNVKVMSVGNMZVOVJVKVMUAVPVKVMVOVPVKSZVMSVLVGTZVLUH
        ZBPZSZVMSZVOVQWAVMVPVRVKVTVPCNTVGNUNVRDVGUINVGCUJUKVKVTVHVSBCVGULUMUOUP
        UQVOVRVMVTQVRVMSVTSWBVGBVLURVRVMVTUSVRVMVTUTVAVBVCVEVDVF $.
        $( [5-May-2007] $) $( [10-Feb-1997] $)
    $}
  $}

  ${
    tz7.49.1 $e |- F Fn On $.
    $d x y z A $.  $d x y z F $.  $d ph y z $.
    tz7.49.2 $e |- ( ph <-> A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                               ( F ` x ) e. ( A \ ( F " x ) ) ) ) $.
    $( Proposition 7.49 of [TakeutiZaring] p. 51.  (Revised by Mario Carneiro,
       10-Jan-2013.) $)
    tz7.49 $p |- ( ( A e. B /\ ph ) ->
                     E. x e. On ( A. y e. x ( A \ ( F " y ) ) =/= (/) /\
                     ( F " x ) = A /\ Fun `' ( F |` x ) ) ) $=
      ( vz wcel c0 wral wceq con0 wrex wa wn wi wss ax-17 cv cima cdif wne cres
      ccnv wfun w3a cfv df-ne ralim sylbi syl5bir cvv tz7.48-3 elex nsyl3 nsyli
      ralbii dfrex2 syl6ibr imaeq2 difeq2d eqeq1d onminex anbi2i rexbii hbxfrbi
      syl6 hbra1 simpllr wfn fnfun ax-mp fvelima mpan ra4 adantld onelon neeq1d
      fveq2 eleq12d imbi12d rcla4v syl5bi com23 syl sylcom com3r imp exp3acom23
      hban eldifi eleq1 syl5ibcom syl8 com34 rexlimd syl5 ssrdv biimpri anim12i
      ssdif0 eqss onss ssel cdm fndm syl6sseqr funfvima2 sylancr com12 a1i ee23
      sylibr imp4a eldifn eleq1a con3d syl5com syldd com4r ralrimi ex tz7.48lem
      imp31 ancld syl56 ancoms adantr 3jca exp41 reximdai syld impcom ) ADEJZDF
      CUAZUBZUCZKUDZCBUAZLZFUUAUBZDMZFUUAUEUFUGZUHZBNOZAYPDUUCUCZKMZUUBPZBNOZUU
      GAYPUUIYSKMZQZCUUALZPZBNOZUUKAYPUUIBNOZUUPAYPUUIQZBNLZQUUQAUUSUUAFUIZUUHJ
      ZBNLZYPUUSUUHKUDZBNLZAUVBUVCUURBNUUHKUJUSAUVCUVARZBNLZUVDUVBRHUVCUVABNUKU
      LUMUVBDUNJYPBDFGUODEUPUQURUUIBNUTVAUUIUULBCUUAYQMZUUHYSKUVGUUCYRDUUAYQFVB
      VCZVDVEVIUUJUUOBNUUBUUNUUIYTUUMCUUAYSKUJUSVFVGVAAUUJUUFBNAUVFBHUVEBNVJVHA
      UUANJZUUIUUBUUFAUVIUUBUUIUUFAUUBUVIUUIUUFRAUUBUVIUUIUUFAUUBPZUVIPZUUIPZUU
      BUUDUUEAUUBUVIUUIVKUVLUUCDSZDUUCSZPUUDUVKUVMUUIUVNUVKIUUCDUVJUVIIUAZUUCJZ
      UVODJZRUVJUVPUVIUVQUVPYQFUIZUVOMZCUUAOZUVJUVIUVQRZFUGZUVPUVTFNVLZUWBGNFVM
      VNZCUVOUUAFVOVPUVJUVSUWACUUAAUUBCACTZYTCUUAVJZWLUWACTUVJYQUUAJZUVIUVSUVQU
      VJUWGUVIUVRYSJZUVSUVQRUVJUVIUWGUWHAUUBUVIUWGPZUWHRUUBUWIAUWHUUBUWIYTAUWHR
      ZUUBUWGYTUVIYTCUUAVQZVRUWIYQNJZYTUWJRUUAYQVSUWLAYTUWHAUVFUWLYTUWHRZHUVEUW
      MBYQNUVGUVCYTUVAUWHUVGUUHYSKUVHVTUVGUUTUVRUUHYSUUAYQFWAUVHWBWCWDWEWFZWGWH
      WIWJWKUWHUVRDJUVSUVQUVRDYRWMUVRUVODWNWOWPWQWRWSWFWJWTUVNUUIDUUCXCXAXBUUCD
      XDXOUVKUUEUUIUVJUVIUUEUUBAUVIUUERUVIUUANSZUUBAPZUWOUVRUVOFUIZMZQZIYQLZCUU
      ALZPUUEUUAXEUWPUWOUXAUWPUWOUXAUWPUWOPZUWTCUUAUWPUWOCUUBACUWFUWEWLUWOCTWLU
      XBUWGUWTUXBUWGPZUWSIYQUXCITUWPUWOUWGUVOYQJZUWSRUWOUWGUXDUWPUWSUWOUWGUXDUW
      QYRJZUWPUWSRUWOUWGUWLUXDUXERZUUANYQXFZUWLUWBYQFXGZSUXFUWDUWLYQNUXHYQXEUWC
      UXHNMGNFXHVNXIYQUVOFXJXKVIUWOUWGUWPUXEUWSUWOUWGUWPUWHUXEUWSRUWOUWGUUBAUWH
      UWOUWGUWLUUBYTUWJUXGUWGUUBYTRRUWOUUBUWGYTUWKXLXMUWNXNXPUWHUVRYRJZQUXEUWSU
      VRDYRXQUXEUWRUXIUWQYRUVRXRXSXTWPWQYAYBYFYCYDYCYDYGCIUUAFGYEYHYIWJYJYKYLWF
      WQXPYMYNYO $.
      $( [10-Jan-2013] $) $( [10-Feb-1997] $)
  $}

  ${
    $d A x y $.  $d F x y $.
    tz7.49c.1 $e |- F Fn On $.
    $( Corollary of Proposition 7.49 of [TakeutiZaring] p. 51.  (Revised by
       Mario Carneiro, 19-Jan-2013.) $)
    tz7.49c $p |- ( ( A e. B /\ A. x e. On ( ( A \ ( F " x ) ) =/= (/) ->
                 ( F ` x ) e. ( A \ ( F " x ) ) ) ) ->
                 E. x e. On ( F |` x ) : x -1-1-onto-> A ) $=
      ( vy wcel cv cima cdif c0 wne cfv con0 wral wa wceq w3a wrex wfn cres crn
      wi ccnv wfun wf1o biid tz7.49 3simpc fnssres sylancr df-ima eqeq1i biimpi
      wss onss anim12i anim1i dff1o2 df-3an an32 3bitri sylibr expl reximia syl
      syl5 ) BCGBDAHZIZJZKLVHDMVJGUCANOZPBDFHIJKLFVHOZVIBQZDVHUAZUDUEZRZANSVHBV
      NUFZANSVKAFBCDEVKUGUHVPVQANVPVMVOPVHNGZVQVLVMVOUIVRVMVOVQVRVMPZVOPVNVHTZV
      NUBZBQZPZVOPZVQVSWCVOVRVTVMWBVRDNTVHNUOVTEVHUPNVHDUJUKVMWBVIWABDVHULUMUNU
      QURVQVTVOWBRVTVOPWBPWDVHBVNUSVTVOWBUTVTVOWBVAVBVCVDVGVEVF $.
      $( [19-Jan-2013] $) $( [10-Feb-1997] $)
  $}

