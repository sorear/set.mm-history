$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                 Axiom of Infinity equivalents
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $( First we prove the equivalence of our version of Infinity with
     some others. $)

  ${
    $d x y z w v u f $.
    inf0.1 $e |- om e. _V $.
    $( Our Axiom of Infinity derived from existence of omega.  The proof shows
       that the especially contrived
class " ` ran ( rec ( { <. v , u >. | u = suc v } , x ) |`` om ) ` " exists, is
       a subset of its union, and contains a given set ` x ` (and thus is
       non-empty).  Thus it provides an example demonstrating that a set ` y `
       exists with the necessary properties demanded by ~ ax-inf . $)
    inf0 $p |- E. y ( x e. y /\ A. z ( z e. y -> E. w ( z e. w /\ w e. y )
                   ) ) $=
      ( vu vv vf cv csuc wceq com wcel wa wex wi c0 cvv ax-mp eleq2 crn wal cfv
      copab crdg cres vex fr0g wfn frfnom peano1 fnfvelrn eqeltrri wrex fvelrnb
      mp2an wb fvex sucid sucex ax-17 hbopab1 hbrdg hbres hbfv hbsuc eqid suceq
      frsucopab mpan2 syl5eleqr eleq1 syl5ib peano2b mpan sylbi a1i jcad cla4ev
      anbi12d syl6com rexlimiv ax-gen cdm wfun eqeltri fnfun funrnex mp2 anbi2d
      fndm exbidv imbi12d albidv ) AIZFIGIZJZKZGFUDZWOUEZLUFZUAZMZCIZXBMZXDDIZM
      ZXFXBMZNZDOZPZCUBZWOBIZMZXDXMMZXGXFXMMZNZDOZPZCUBZNZBOQXAUCZWOXBWORMYBWOK
      AUGWORWSUHSXALUIZQLMYBXBMWOWSUJZUKLQXAULUPUMXKCXEHIZXAUCZXDKZHLUNZXJYCXEY
      HUQYDHLXDXAUOSYGXJHLYGYELMZXDYEJZXAUCZMZYKXBMZNZXJYGYIYLYMYIYFYKMYGYLYIYF
      YFJZYKYFYEXAURZUSYIYORMYKYOKYFYPUTGFCWOYEWQYORXAXDWOMGVAZXDYEMGVAZGCYFGCY
      EXAGCWTLGCWOWSWRGFCVBYQVCXDLMGVAVDYRVEVFXAVGWPYFVHVIVJVKYFXDYKVLVMYIYMPYG
      YIYJLMZYMYEVNYCYSYMYDLYJXAULVOVPVQVRXIYNDYKYJXAURXFYKKXGYLXHYMXFYKXDTXFYK
      XBVLVTVSWAWBVPWCYAXCXLNBXBXAWDZRMXAWEZXBRMYTLRYCYTLKYDLXAWKSEWFYCUUAYDLXA
      WGSRXAWHWIXMXBKZXNXCXTXLXMXBWOTUUBXSXKCUUBXOXEXRXJXMXBXDTUUBXQXIDUUBXPXHX
      GXMXBXFTWJWLWMWNVTVSUP $.
      $( [2-Apr-2007] $) $( [15-Oct-1996] $)
  $}

  ${
    inf1.1 $e |- E. x ( y e. x /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $.
    $( Variation of Axiom of Infinity (using ~ zfinf as a hypothesis).  Axiom
       of Infinity in [FreydScedrov] p. 283.  (Distinct variable requirements
       removed by David Abernethy, 1-Oct-2013.) $)
    inf1 $p |- E. x ( x =/= (/) /\
                 A. y ( y e. x -> E. z ( y e. z /\ z e. x ) ) ) $=
      ( cv wcel wa wex wi wal c0 wne ne0i anim1i eximi ax-mp ) BEZAEZFZSQCEZFTR
      FGCHIBJZGZAHRKLZUAGZAHDUBUDASUCUARQMNOP $.
      $( [3-Oct-2013] $) $( [14-Oct-1996] $)

    $d x y z $.
    $( Variation of Axiom of Infinity.  There exists a non-empty set that is a
       subset of its union (using ~ zfinf as a hypothesis).  Abbreviated
       version of the Axiom of Infinity in [FreydScedrov] p. 283. $)
    inf2 $p |- E. x ( x =/= (/) /\ x C_ U. x ) $=
      ( cv c0 wne cuni wss wa wex wcel wal inf1 dfss2 eluni imbi2i albii bitri
      wi anbi2i exbii mpbir ) AEZFGZUDUDHZIZJZAKUEBEZUDLZUICEZLUKUDLJCKZTZBMZJZ
      AKABCDNUHUOAUGUNUEUGUJUIUFLZTZBMUNBUDUFOUQUMBUPULUJCUIUDPQRSUAUBUC $.
      $( [11-Apr-2007] $) $( [28-Oct-1996] $)
  $}

  ${
    $d x y z w v u f g $.  $d v u f g G $.  $d v u f g F $.  $d v u f g A $.
    $d v u f g B $.
    inf3lem.1 $e |- G =
         { <. y , z >. | z = { w e. x | ( w i^i x ) C_ y } } $.
    inf3lem.2 $e |- F = ( rec ( G , (/) ) |` om ) $.
    inf3lem.3 $e |- A e. _V $.
    inf3lem.4 $e |- B e. _V $.
    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lema $p |- ( A e. ( G ` B ) <-> ( A e. x /\ ( A i^i x ) C_ B ) ) $=
      ( vf vu vv cv cin wss wceq crab cfv ineq1 sseq1d copab id rabbidv cbvrabv
      sseq2 syl6eq eqeqan12rd cbvopabv eqtri fveq1i vex rabex fvopab elrab2 ) M
      PZAPZQZFRZEUSQZFRMEUSFHUAZURESUTVBFUREUSUBUCVCFNPZUTOPZRZMUSTZSZONUDZUAVA
      MUSTZFHVIHCPZDPZUSQZBPZRZDUSTZSZBCUDVIIVQVHBCONVKVDSZVNVESZVKVDVPVGVRUEVS
      VPVMVERZDUSTVGVSVOVTDUSVNVEVMUHUFVTVFDMUSVLURSVMUTVEVLURUSUBUCUGUIUJUKULU
      MONFVGVJLVAMUSAUNUOVEFSVFVAMUSVEFUTUHUFUPULUQ $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemb $p |- ( F ` (/) ) = (/) $=
      ( c0 cfv crdg com cres fveq1i cvv wcel wceq 0ex fr0g ax-mp eqtri ) MGNMHM
      OPQZNZMMGUFJRMSTUGMUAUBMSHUCUDUE $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemc $p |- ( A e. om -> ( F ` suc A ) = ( G ` ( F ` A ) ) ) $=
      ( com wcel csuc c0 crdg cres cfv fveq1i frsuc fveq2i 3eqtr4g ) EMNEOZHPQM
      RZSEUESZHSUDGSEGSZHSPEHUAUDGUEJTUGUFHEGUEJTUBUC $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lemd $p |- ( A e. om -> ( F ` A ) C_ x ) $=
      ( vv vu com wcel cfv cv wss c0 wi 0ss fveq2 inf3lemb syl6eq sseq1d mpbiri
      wceq a1d wne wa csuc wrex nnsuc vex inf3lemc eleq2d fvex inf3lema simplbi
      cin syl6bi ssrdv syl5ibrcom rexlimiv syl expcom pm2.61ine ) EOPZEGQZARZSZ
      UAETETUHZVLVIVMVLTVKSVKUBVMVJTVKVMVJTGQTETGUCABCDEFGHIJKLUDUEUFUGUIVIETUJ
      ZVLVIVNUKEMRZULZUHZMOUMVLMEUNVQVLMOVOOPZVLVQVPGQZVKSVRNVSVKVRNRZVSPVTVOGQ
      ZHQZPZVTVKPZVRVSWBVTABCDVOFGHIJMUOLUPUQWCWDVTVKVAWASABCDVTWAGHIJNUOVOGURU
      SUTVBVCVQVJVSVKEVPGUCUFVDVEVFVGVH $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem1 $p |- ( A e. om -> ( F ` A ) C_ ( F ` suc A ) ) $=
      ( vv cfv csuc wss c0 wceq fveq2 wcel vu suceq fveq2d sseq12d inf3lemb 0ss
      cv eqsstri com wa wi cin sstr2 com12 anim2d inf3lemc eleq2d fvex inf3lema
      vex syl6bb peano2b sucex sylbi imbi12d syl5ibr imp ssrdv ex finds ) MUGZG
      NZVKOZGNZPQGNZQOZGNZPUAUGZGNZVROZGNZPZWAVTOZGNZPZEGNZEOZGNZPMUAEVKQRZVLVO
      VNVQVKQGSWIVMVPGVKQUBUCUDVKVRRZVLVSVNWAVKVRGSWJVMVTGVKVRUBUCUDVKVTRZVLWAV
      NWDVKVTGSWKVMWCGVKVTUBUCUDVKERZVLWFVNWHVKEGSWLVMWGGVKEUBUCUDVOQVQABCDEEGH
      IJKKUEVQUFUHVRUITZWBWEWMWBUJMWAWDWMWBVKWATZVKWDTZUKZWBWPWMVKAUGZTZVKWQULZ
      VSPZUJZWRWSWAPZUJZUKWBWTXBWRWTWBXBWSVSWAUMUNUOWMWNXAWOXCWMWNVKVSHNZTXAWMW
      AXDVKABCDVREGHIJUAUTZKUPUQABCDVKVSGHIJMUTZVRGURUSVAWMWOVKWAHNZTXCWMWDXGVK
      WMVTUITWDXGRVRVBABCDVTEGHIJVRXEVCKUPVDUQABCDVKWAGHIJXFVTGURUSVAVEVFVGVHVI
      VJ $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem2 $p |- ( ( x =/= (/) /\ x C_ U. x ) ->
                    ( A e. om -> ( F ` A ) =/= x ) ) $=
      ( vv wcel c0 wne wa cfv wi wceq vu vf com cv cuni wss fveq2 neeq1d imbi2d
      csuc inf3lemb eqeq1i eqcom biimpi necon3i adantr vex inf3lemd wn wex wpss
      bitri df-pss pssnel sylbir ssel eluni syl6ib biimparc inf3lemc eleq2d cin
      eleq2 elin fvex simprbi sseld syl5bir syl6bi syl5 com23 exp5c com34 imp3a
      inf3lema exlimdv sylan9r pm2.43d id necon3bd sylani exp4b pm2.43a adantld
      syl6 a2d finds com12 ) EUCNAUDZOPZWSWSUEZUFZQZEGRZWSPZXCMUDZGRZWSPZSXCOGR
      ZWSPZSXCUAUDZGRZWSPZSXCXKUJZGRZWSPZSXCXESMUAEXFOTZXHXJXCXQXGXIWSXFOGUGUHU
      IXFXKTZXHXMXCXRXGXLWSXFXKGUGUHUIXFXNTZXHXPXCXSXGXOWSXFXNGUGUHUIXFETZXHXEX
      CXTXGXDWSXFEGUGUHUIWTXJXBXIWSWSOXIWSTZWSOTZYAOWSTYBXIOWSABCDEFGHIJKLUKULO
      WSUMVBUNUOUPXKUCNZXCXMXPYCXBXMXPSZWTXBYCYDYCXBYCXMXPYCYCXBQZXLWSUFZXMXPAB
      CDXKFGHIJUAUQZLURYFXMQZXFWSNZXFXLNZUSZQZMUTZYEXPYHXLWSVAYMXLWSVCMXLWSVDVE
      YEYLXPMYEYIYKXPYEYIXOWSTZYJSZYKXPSYEYIYOXBYIXFUBUDZNZYPWSNZQZUBUTZYCYIYOS
      ZXBYIXFXANYTWSXAXFVFUBXFWSVGVHYCYSUUAUBYCYQYRUUAYCYQYIYRYOYCYQYIYRYNYJYCY
      RYNQZYQYIQZYJUUBYPXONZYCUUCYJSZYNUUDYRXOWSYPVMVIYCUUDYPXLHRZNZUUEYCXOUUFY
      PABCDXKFGHIJYGLVJVKUUCXFYPWSVLZNUUGYJXFYPWSVNUUGUUHXLXFUUGYRUUHXLUFABCDYP
      XLGHIJUBUQXKGVOWEVPVQVRVSVTWAWBWCWDWFWGWHYOYJXOWSYOWIWJWOWDWFVTWKWLWMWNWP
      WQWR $.
      $( [28-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description.  In the proof, we invoke the Axiom of
       Regularity in the form of ~ zfreg . $)
    inf3lem3 $p |- ( ( x =/= (/) /\ x C_ U. x ) ->
                    ( A e. om -> ( F ` A ) =/= ( F ` suc A ) ) ) $=
      ( vv wcel cv c0 wne wss wa cfv com cuni csuc cdif inf3lem2 com12 inf3lemd
      jctild pssdifn0 syl6 cin wceq wrex vex difss ssexi zfreg inssdif0 biimpri
      eldifi anim12i fvex inf3lema sylibr inf3lemc eleq2d syl5ibr eldifn adantr
      wn wi a1i jcad eleq2 biimprd iman sylib necon2ai exp3a rexlimdv syl5 syld
      ) EUANZAOZPQWDWDUBRSZEGTZEUCGTZQZWCWEWDWFUDZPQZWHWCWEWFWDRZWFWDQZSWJWCWEW
      LWKWEWCWLABCDEFGHIJKLUEUFABCDEFGHIJKLUGUHWFWDUIUJWJMOZWIUKPULZMWIUMWCWHMW
      IWIWDAUNWDWFUOUPUQWCWNWHMWIWCWMWINZWNWHWCWOWNSZWMWGNZWMWFNZVJZSZWHWCWPWQW
      SWPWQWCWMWFHTZNZWPWMWDNZWMWDUKWFRZSXBWOXCWNXDWMWDWFUTXDWNWMWDWFURUSVAABCD
      WMWFGHIJMUNEGVBVCVDWCWGXAWMABCDEFGHIJKLVEVFVGWPWSVKWCWOWSWNWMWDWFVHVIVLVM
      WTWFWGWFWGULZWQWRVKWTVJXEWRWQWFWGWMVNVOWQWRVPVQVRUJVSVTWAWBUF $.
      $( [29-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem4 $p |- ( ( x =/= (/) /\ x C_ U. x ) ->
                    ( A e. om -> ( F ` A ) C. ( F ` suc A ) ) ) $=
      ( cv c0 wne cuni wss wa com cfv wcel csuc wpss inf3lem1 a1i inf3lem3 jcad
      wi df-pss syl6ibr ) AMZNOUKUKPQRZESUAZEGTZEUBGTZQZUNUOOZRUNUOUCULUMUPUQUM
      UPUHULABCDEFGHIJKLUDUEABCDEFGHIJKLUFUGUNUOUIUJ $.
      $( [29-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem5 $p |- ( ( x =/= (/) /\ x C_ U. x ) ->
                    ( ( A e. om /\ B e. A ) -> ( F ` B ) C. ( F ` A ) ) ) $=
      ( com wcel wa cfv wpss wi wceq fveq2 vv vu cv c0 wne cuni wss elnn ancoms
      csuc word nnord ordsucss syl adantr peano2b psseq2d imbi2d inf3lem4 com12
      sylbir vex psstr expcom syl6com a2d ad2antrr findsg sylan2b syld impancom
      ex mpd ) EMNZFENZOZAUCZUDUEVQVQUFUGOZFGPZEGPZQZVPFMNZVRWARZVOVNWBFEUHUIVN
      WBVOWCVNWBOVOFUJZEUGZWCVNVOWERZWBVNEUKWFEULFEUMUNUOWBVNWDMNZWEWCRFUPZVNWG
      OWEWCVRVSUAUCZGPZQZRVRVSWDGPZQZRZVRVSUBUCZGPZQZRZVRVSWOUJZGPZQZRZWCUAUBEW
      DWIWDSZWKWMVRXCWJWLVSWIWDGTUQURWIWOSZWKWQVRXDWJWPVSWIWOGTUQURWIWSSZWKXAVR
      XEWJWTVSWIWSGTUQURWIESZWKWAVRXFWJVTVSWIEGTUQURWGWBWNWHVRWBWMABCDFFGHIJLLU
      SUTVAWOMNZWRXBRWGWDWOUGXGVRWQXAVRXGWPWTQZWQXARABCDWOFGHIJUBVBLUSWQXHXAVSW
      PWTVCVDVEVFVGVHVLVIVJVKVMUT $.
      $( [29-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description. $)
    inf3lem6 $p |- ( ( x =/= (/) /\ x C_ U. x ) -> F : om -1-1-> ~P x ) $=
      ( vv vu cv wss wa com wceq wcel c0 wne cuni cpw wf cfv wi wral wo wn wpss
      wf1 vex inf3lem5 dfpss2 simprbi syl6 expdimp adantrl eqcom sylnib adantrr
      jaod con2d wb word nnord ordtri3 syl2an adantl sylibrd ralrimivva wfn crn
      crdg cres frfnom fneq1 mpbiri wrex fvelrnb inf3lemd fvex sylibr syl5ibcom
      elpw eleq1 rexlimiv syl6bi ssrdv ancli mp2b df-f mpbir jctil dff13 ) AOZU
      AUBWQWQUCPQZRWQUDZGUEZMOZGUFZNOZGUFZSZXAXCSZUGZNRUHMRUHZQRWSGULWRXHWTWRXG
      MNRRWRXARTZXCRTZQZQZXEXAXCTZXCXATZUIZUJZXFXLXOXEXLXMXEUJZXNWRXJXMXQUGXIWR
      XJXMXQWRXJXMQXBXDUKZXQABCDXCXAGHIJNUMZMUMZUNXRXBXDPXQXBXDUOUPUQURUSWRXIXN
      XQUGXJWRXIXNXQWRXIXNQXDXBUKZXQABCDXAXCGHIJXTXSUNYAXDXBSZXEYAXDXBPYBUJXDXB
      UOUPXDXBUTVAUQURVBVCVDXKXFXPVEZWRXIXAVFXCVFYCXJXAVGXCVGXAXCVHVIVJVKVLWTGR
      VMZGVNZWSPZQZGHUAVORVPZSZYDYGJYIYDYHRVMUAHVQRGYHVRVSYDYFYDNYEWSYDXCYETXBX
      CSZMRVTXCWSTZMRXCGWAYJYKMRXIXBWSTZYJYKXIXBWQPYLABCDXAFGHIJXTLWBXBWQXAGWCW
      FWDXBXCWSWGWEWHWIWJWKWLRWSGWMWNWOMNRWSGWPWD $.
      $( [29-Oct-1996] $)

    $( Lemma for our Axiom of Infinity => standard Axiom of Infinity.  See
       ~ inf3 for detailed description.  In the proof, we invoke the Axiom of
       Replacement in the form of ~ f1dmex .  (The proof was shortened by Mario
       Carneiro, 19-Jan-2013.) $)
    inf3lem7 $p |- ( ( x =/= (/) /\ x C_ U. x ) -> om e. _V ) $=
      ( cv c0 wne cuni wss com cvv wcel wa cpw wf1 inf3lem6 pwex f1dmex sylancl
      vex ) AMZNOUIUIPQUARUIUBZGUCUJSTRSTABCDEFGHIJKLUDUIAUHUERUJSGUFUG $.
      $( [19-Jan-2013] $) $( [29-Oct-1996] $)
  $}

  ${
    $d x y z w $.
    inf3.1 $e |- E. x ( x =/= (/) /\ x C_ U. x ) $.
    $( Note: the special <HTML> tag is handled in mmwtex.c.  All comment text
       between <HTML> and </HTML> is passed as-is to the web page, except
       that ` (symbols) ` , ~ (label) , and [(reference)] markups are still
       interpreted. $)
    $( Our Axiom of Infinity ~ ax-inf implies the standard Axiom of Infinity.
       The hypothesis is a variant of our Axiom of Infinity provided by
       ~ inf2 , and the conclusion is the version of the Axiom of Infinity
       shown as Axiom 7 in [TakeutiZaring] p. 43.  (Other standard versions are
       proved later as ~ axinf2 and ~ zfinf2 .)  The main proof is provided by
       ~ inf3lema through ~ inf3lem7 , and this final piece eliminates the
       auxiliary hypothesis of ~ inf3lem7 .  This proof is due to
       Ian Sutherland, Richard Heck, and Norman Megill and was posted
       on Usenet as shown below.  Although the result is not new, the authors
       were unable to find a published proof.
       <HTML>
       <PRE>
       (As posted to sci.logic on 30-Oct-1996, with annotations added.)

       Theorem:  The statement "There exists a non-empty set that is a subset
       of its union" implies the Axiom of Infinity.

       Proof:  Let X be a nonempty set which is a subset of its union; the
       latter
       property is equivalent to saying that for any y in X, there exists a z
       in X
       such that y is in z.

       Define by finite recursion a function F:omega-->(power X) such that
       F_0 = 0  (See ~ inf3lemb .)
       F_n+1 = {y&lt;X | y^X subset F_n}  (See ~ inf3lemc .)
       Note: ^ means intersect, &lt; means \in ("element of").
       (Finite recursion as typically done requires the existence of omega;
       to avoid this we can just use transfinite recursion restricted to omega.
       F is a class-term that is not necessarily a set at this point.)

       Lemma 1.  F_n subset F_n+1.  (See ~ inf3lem1 .)
       Proof:  By induction:  F_0 subset F_1.  If y &lt; F_n+1, then y^X subset
       F_n,
       so if F_n subset F_n+1, then y^X subset F_n+1, so y &lt; F_n+2.

       Lemma 2.  F_n =/= X.  (See ~ inf3lem2 .)
       Proof:  By induction:  F_0 =/= X because X is not empty.  Assume F_n =/=
       X.
       Then there is a y in X that is not in F_n.  By definition of X, there is
       a
       z in X that contains y.  Suppose F_n+1 = X.  Then z is in F_n+1, and z^X
       contains y, so z^X is not a subset of F_n, contrary to the definition of
       F_n+1.

       Lemma 3.  F_n =/= F_n+1.  (See ~ inf3lem3 .)
       Proof:  Using the identity y^X subset F_n &lt;-> y^(X-F_n) = 0, we have
       F_n+1 = {y&lt;X | y^(X-F_n) = 0}.  Let q = {y&lt;X-F_n | y^(X-F_n) = 0}.
       Then q subset F_n+1.  Since X-F_n is not empty by Lemma 2 and q is the
       set of \in-minimal elements of X-F_n, by Foundation q is not empty, so q
       and therefore F_n+1 have an element not in F_n.

       Lemma 4.  F_n proper_subset F_n+1.  (See ~ inf3lem4 .)
       Proof:  Lemmas 1 and 3.

       Lemma 5.  F_m proper_subset F_n, m &lt; n.  (See ~ inf3lem5 .)
       Proof:  Fix m and use induction on n > m.  Basis: F_m proper_subset
       F_m+1
       by Lemma 4.  Induction:  Assume F_m proper_subset F_n.  Then since F_n
       proper_subset F_n+1, F_m proper_subset F_n+1 by transitivity of proper
       subset.

       By Lemma 5, F_m =/= F_n for m =/= n, so F is 1-1.  (See ~ inf3lem6 .)
       Thus the inverse of F is a function with range omega and domain a subset
       of power X, so omega exists by Replacement.  (See ~ inf3lem7 .)
       Q.E.D.
       </PRE> </HTML> $)
    inf3 $p |- om e. _V $=
      ( vy vz vw cv c0 wne cuni wss wex com cvv wcel cin crab wceq copab eqid
      wa crdg cres vex inf3lem7 exlimiv ax-mp ) AFZGHUGUGIJTZAKLMNZBUHUIAACDEUG
      UGDFEFUGOCFJEUGPQCDRZGUALUBZUJUJSUKSAUCZULUDUEUF $.
      $( [29-Oct-1996] $)
  $}

  ${
    $d x y z w $.
    $( The statement "there exists a set that is a proper subset of its union"
       is equivalent to the Axiom of Infinity (shown on the right-hand side in
       the form of ~ omex .)  The left-hand side provides us with a very short
       way to express of the Axiom of Infinity using only elementary symbols.
       This proof of equivalence does not depend on the Axiom of Infinity. $)
    infeq5 $p |- ( E. x x C. U. x <-> om e. _V ) $=
      ( vy vz vw cv cuni wpss wex com cvv wcel c0 wne wss wceq unieq sylbi eqid
      wa cin df-pss uni0 syl6req eqtr mpdan anim1i ancoms eximi crab copab crdg
      necon3i cres vex inf3lem7 exlimiv syl csn cdif difexg snid wn disj4 disj3
      0ex bitr3i peano1 eleq2 mpbii eldif sylib simprd mt4 unidif0 limom limuni
      wlim ax-mp eqtr4i psseq2i mpbir psseq1 psseq2d bitrd cla4egv mpi impbii )
      AEZWHFZGZAHZIJKZWKWHLMZWHWINZSZAHWLWJWOAWJWNWHWIMZSWOWHWIUAWPWNWOWPWMWNWH
      LWHWIWHLOZLWIOWHWIOWQWILFLWHLPUBUCWHLWIUDUEULUFUGQUHWOWLAABCDWHWHCEDEWHTB
      ENDWHUIOBCUJZLUKIUMZWRWRRWSRAUNZWTUOUPUQWLILURZUSZJKZWKIXAJUTXCXBXBFZGZWK
      XEXBIGZLXAKZXFLVEVAXFVBZIXBOZXGVBZXHIXATLOXIIXAVCIXAVDVFXILIKZXJXILXBKZXK
      XJSXIXKXLVGIXBLVHVILIXAVJVKVLQVMXDIXBXDIFZIIVNIVQIXMOVOIVPVRVSVTWAWJXEAXB
      JWHXBOZWJXBWIGXEWHXBWIWBXNWIXDXBWHXBPWCWDWEWFUQWG $.
      $( [23-Mar-2004] $)
  $}

