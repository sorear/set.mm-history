$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Miscellany
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    a1i13.1 $e |- ( ps -> th ) $.
    $( Add two antecedents to a wff. $)
    a1i13 $p |- ( ph -> ( ps -> ( ch -> th ) ) ) $=
      ( wi a1d a1i ) BCDFFABDCEGH $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i4.1 $e |- ( ph -> ( ps -> ( ch -> ta ) ) ) $.
    $( Add an antecedent to a wff. $)
    a1i4 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi ax-1 syl8 ) ABCEDEGFEDHI $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i14.1 $e |- ( ps -> ( ch -> ta ) ) $.
    $( Add two antecedents to a wff. $)
    a1i14 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd a1i ) BCDEGGGABCEDFHI $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i24.1 $e |- ( ph -> ( ch -> ta ) ) $.
    $( Add two antecedents to a wff. $)
    a1i24 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd a1d ) ACDEGGBACEDFHI $.
      $( [5-Aug-2009] $)
  $}

  ${
    a1i34.1 $e |- ( ph -> ( ps -> ta ) ) $.
    $( Add two antecedents to a wff. $)
    a1i34 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd ) ABDEGCABEDFHH $.
      $( [5-Aug-2009] $)
  $}

  ${
    impbid3.1OLD $e |- ( ph -> ( ps -> ch ) ) $.
    impbid3.2OLD $e |- ( ph -> ( -. ps -> -. ch ) ) $.
    $( A variation on ~ impbid .  (Moved to ~ impcon4bid in main set.mm and may
       be deleted by mathbox owner, JGH. --NM 26-Aug-2011.) $)
    impbid3OLD $p |- ( ph -> ( ps <-> ch ) ) $=
      ( impcon4bid ) ABCDEF $.
      $( [3-Jul-2009] $)
  $}

  ${
    imp5.1 $e |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $.
    $( An importation inference. $)
    imp5g $p |- ( ( ph /\ ps ) -> ( ( ( ch /\ th ) /\ ta ) -> et ) ) $=
      ( wa wi imp imp4c ) ABHCDEFABCDEFIIIGJK $.
      $( [7-Jul-2009] $)

    $( An importation inference. $)
    imp55 $p |- ( ( ( ph /\ ( ps /\ ( ch /\ th ) ) ) /\ ta ) -> et ) $=
      ( wa wi imp4a imp42 ) ABCDHEFABCDEFIGJK $.
      $( [7-Jul-2009] $)

    $( An importation inference. $)
    imp511 $p |- ( ( ph /\ ( ( ps /\ ( ch /\ th ) ) /\ ta ) ) -> et ) $=
      ( wa wi imp4a imp44 ) ABCDHEFABCDEFIGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5c.1 $e |- ( ph -> ( ( ps /\ ch ) -> ( ( th /\ ta ) -> et ) ) ) $.
    $( An exportation inference. $)
    exp5c $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp4a exp3a ) ABCDEFHHABCIDEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5d.1 $e |- ( ( ( ph /\ ps ) /\ ch ) -> ( ( th /\ ta ) -> et ) ) $.
    $( An exportation inference. $)
    exp5d $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp31 ) ABCDEFHHABICIDEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5g.1 $e |- ( ( ph /\ ps ) -> ( ( ( ch /\ th ) /\ ta ) -> et ) ) $.
    $( An exportation inference. $)
    exp5g $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp4c ex ) ABCDEFHHHABICDEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5j.1 $e |- ( ph -> ( ( ( ( ps /\ ch ) /\ th ) /\ ta ) -> et ) ) $.
    $( An exportation inference. $)
    exp5j $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp4c ) ABCDEFHABCIDIEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5k.1 $e |- ( ph -> ( ( ( ps /\ ( ch /\ th ) ) /\ ta ) -> et ) ) $.
    $( An exportation inference. $)
    exp5k $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp4d ) ABCDEFHABCDIIEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5l.1 $e |- ( ph -> ( ( ( ps /\ ch ) /\ ( th /\ ta ) ) -> et ) ) $.
    $( An exportation inference. $)
    exp5l $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa exp3a exp5c ) ABCDEFABCHDEHFGIJ $.
      $( [27-Jan-2010] $) $( [7-Jul-2009] $)
  $}

  ${
    exp56.1 $e |- ( ( ( ( ph /\ ps ) /\ ch ) /\ ( th /\ ta ) ) -> et ) $.
    $( An exportation inference. $)
    exp56 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5d ) ABCDEFABHCHDEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp58.1 $e |- ( ( ( ph /\ ps ) /\ ( ( ch /\ th ) /\ ta ) ) -> et ) $.
    $( An exportation inference. $)
    exp58 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5g ) ABCDEFABHCDHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp510.1 $e |- ( ( ph /\ ( ( ( ps /\ ch ) /\ th ) /\ ta ) ) -> et ) $.
    $( An exportation inference. $)
    exp510 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5j ) ABCDEFABCHDHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp511.1 $e |- ( ( ph /\ ( ( ps /\ ( ch /\ th ) ) /\ ta ) ) -> et ) $.
    $( An exportation inference. $)
    exp511 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5k ) ABCDEFABCDHHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp512.1 $e |- ( ( ph /\ ( ( ps /\ ch ) /\ ( th /\ ta ) ) ) -> et ) $.
    $( An exportation inference. $)
    exp512 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5l ) ABCDEFABCHDEHHFGIJ $.
      $( [27-Jan-2010] $) $( [7-Jul-2009] $)
  $}

  ${
    mtord.1 $e |- ( ph -> -. ch ) $.
    mtord.2 $e |- ( ph -> -. th ) $.
    mtord.3 $e |- ( ph -> ( ps -> ( ch \/ th ) ) ) $.
    $( A modus tollens deduction involving disjunction. $)
    mtord $p |- ( ph -> -. ps ) $=
      ( wn wo wi df-or syl6ib mpid mtod ) ABDFABCHZDEABCDIODJGCDKLMN $.
      $( [15-Jul-2009] $)
  $}

  ${
    3com12d.1 $e |- ( ph -> ( ps /\ ch /\ th ) ) $.
    $( Commutation in consequent.  Swap 1st and 2nd. $)
    3com12d $p |- ( ph -> ( ch /\ ps /\ th ) ) $=
      ( w3a id 3com12 syl ) ABCDFCBDFZECBDJJGHI $.
      $( [17-Nov-2009] $)
  $}

  ${
    3imp5.1 $e |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $.
    $( A triple importation inference. $)
    imp5p $p |- ( ph -> ( ps -> ( ( ch /\ th /\ ta ) -> et ) ) ) $=
      ( w3a wi com52l 3imp com3l ) CDEHABFCDEABFIIABCDEFGJKL $.
      $( [8-Jul-2009] $)

    $( A triple importation inference. $)
    imp5q $p |- ( ( ph /\ ps ) -> ( ( ch /\ th /\ ta ) -> et ) ) $=
      ( wa wi imp 3impd ) ABHCDEFABCDEFIIIGJK $.
      $( [8-Jul-2009] $)
  $}


  ${
    ecase13d.1 $e |- ( ph -> -. ch ) $.
    ecase13d.2 $e |- ( ph -> -. th ) $.
    ecase13d.3 $e |- ( ph -> ( ch \/ ps \/ th ) ) $.
    $( Deduction for elimination by cases. $)
    ecase13d $p |- ( ph -> ps ) $=
      ( wn wo wi w3o 3orass df-or bitri sylib mpd orcom ) ADHZBFABDIZRBJZACHZSE
      ACBDKZUASJZGUBCSIUCCBDLCSMNOPSDBITBDQDBMNOP $.
      $( [18-Aug-2009] $)
  $}

  ${
    $d y ph $.  $d x y ps $.  $d x y A $.
    eqeu.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A condition which implies existential uniqueness. $)
    eqeu $p |- ( ( A e. B /\ ps /\ A. x ( ph -> x = A ) ) -> E! x ph ) $=
      ( vy weu wex cv wceq wi wal wcel w3a ax-17 eu3 cla4egv imp 3adant3 eqeq2
      imbi2d albidv 3adant2 sylanbrc ) ACHACIZACJZGJZKZLZCMZGIZDENZBAUGDKZLZCMZ
      OACGAGPQUMBUFUPUMBUFABCDEFRSTUMUPULBUMUPULUKUPGDEUHDKZUJUOCUQUIUNAUHDUGUA
      UBUCRSUDUE $.
      $( [8-Sep-2009] $)
  $}

  ${
    $d z w ph $.  $d z w ps $.  $d z w ch $.  $d y z w A $.  $d y z w B $.
    $d y z w X $.  $d y z w Y $.  $d y z w Z $.  $d x y z w $.
    subtr.1 $e |- ( y e. A -> A. x y e. A ) $.
    subtr.2 $e |- ( y e. B -> A. x y e. B ) $.
    ${
      subtr.3 $e |- ( y e. Y -> A. x y e. Y ) $.
      subtr.4 $e |- ( y e. Z -> A. x y e. Z ) $.
      subtr.5 $e |- ( x = A -> X = Y ) $.
      subtr.6 $e |- ( x = B -> X = Z ) $.
      $( Transitivity of implicit substitution. $)
      subtr $p |- ( ( A e. C /\ B e. D ) -> ( A = B -> Y = Z ) ) $=
        ( vz vw cv wceq csb wi eqeq1 wex a9e wcel ax-17 hbeq visset hbcsb1
        hbim csbeq1a sylan9req ex sylbird 19.23ai ax-mp eqeq1d imbi12d eqeq2
        eqeq2d csbeq1 vtocl2g ) PRZQRZSZAVCGTZAVDGTZSZUACVDSZHVGSZUACDSZHISZUAP
        QCDEFVCCSZVEVIVHVJVCCVDUBVMVFHVGARZVCSZAUCVMVFHSZUAZAPUDVOVQAVMVPAABBVC
        CBRZVCUEAUFZJUGABBVFHABVCGPUHVSUILUGUJVOVMVNCSZVPVNVCCUBVOVTVPVOVTVFGHA
        VCGUKNULUMUNUOUPUQURVDDSZVIVKVJVLVDDCUSWAVGIHVNVDSZAUCWAVGISZUAZAQUDWBW
        DAWAWCAABBVDDVRVDUEAUFZKUGABBVGIABVDGQUHWEUIMUGUJWBWAVNDSZWCVNVDDUBWBWF
        WCWBWFVGGIAVDGUKOULUMUNUOUPUTURAVCVDGVAVB $.
        $( [13-Sep-2009] $)
    $}

    ${
      subtr2.3 $e |- ( ps -> A. x ps ) $.
      subtr2.4 $e |- ( ch -> A. x ch ) $.
      subtr2.5 $e |- ( x = A -> ( ph <-> ps ) ) $.
      subtr2.6 $e |- ( x = B -> ( ph <-> ch ) ) $.
      $( Transitivity of implicit substitution into a wff. $)
      subtr2 $p |- ( ( A e. C /\ B e. D ) -> ( A = B -> ( ps <-> ch ) ) ) $=
        ( vz vw cv wceq wsbc wb wi eqeq1 wex a9e wcel ax-17 hbeq visset
        hbsbc1v hbbi hbim sbceq1a bicomd sylan9bb ex sylbird 19.23ai ax-mp
        bibi1d imbi12d eqeq2 bibi2d dfsbcq vtocl2g ) PRZQRZSZADVFTZADVGTZUAZUBF
        VGSZBVJUAZUBFGSZBCUAZUBPQFGHIVFFSZVHVLVKVMVFFVGUCVPVIBVJDRZVFSZDUDVPVIB
        UAZUBZDPUEVRVTDVPVSDDEEVFFERZVFUFDUGJUHVIBDADVFPUIUJLUKULVRVPVQFSZVSVQV
        FFUCVRWBVSVRVIAWBBVRAVIADVFUMUNNUOUPUQURUSUTVAVGGSZVLVNVMVOVGGFVBWCVJCB
        VQVGSZDUDWCVJCUAZUBZDQUEWDWFDWCWEDDEEVGGWAVGUFDUGKUHVJCDADVGQUIUJMUKULW
        DWCVQGSZWEVQVGGUCWDWGWEWDVJAWGCWDAVJADVGUMUNOUOUPUQURUSVCVAADVFVGVDVE
        $.
        $( [19-Sep-2009] $)
    $}
  $}

  ${
    $d w A $.  $d z w C $.  $d z w D $.  $d z w x y $.
    cbvcsb.1 $e |- ( z e. C -> A. y z e. C ) $.
    cbvcsb.2 $e |- ( z e. D -> A. x z e. D ) $.
    cbvcsb.3 $e |- ( x = y -> C = D ) $.
    $( Change bound variables in a class substitution.  Interestingly, this
       does not require any bound variable conditions on ` A ` . $)
    cbvcsb $p |- ( A e. B -> [_ A / x ]_ C = [_ A / y ]_ D ) $=
      ( vw cv csb wceq csbeq1 eqeq12d visset cvv wcel wal wi ax-17 hbcsbg
      ax-mp hbcsb1 eqcomd eqcoms csbeq1a subtr mp2an csbief vtoclg ) AKLZFMZBUM
      GMZNADFMZBDGMZNKDEUMDNUNUPUOUQAUMDFOBUMDGOPACUMFUOKQZUMRSZCLZUOSZVAATUAUR
      ABCUMGRUTUMSZAUBIUCUDALZRSUSVCUMNFUONUAAQURBCVCUMRRGFUOUTVCSBUBVBBUBZHBCU
      MGURVDUEGFNVCBLZVCVENFGJUFUGBUMGUHUIUJUKUL $.
      $( [13-Sep-2009] $)
  $}

  ${
    $d z w ph $.  $d z w ps $.  $d z w A $.  $d z w B $.  $d z w x y $.
    cbvsbc.1OLD $e |- ( ph -> A. y ph ) $.
    cbvsbc.2OLD $e |- ( ps -> A. x ps ) $.
    cbvsbc.3OLD $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( Change bound variables in a wff substitution.  (Moved to ~ cbvsbc in
       main set.mm and may be deleted by mathbox owner, JGH. --NM
       25-Aug-2011.) $)
    cbvsbcOLD $p |- ( A e. B -> ( [ A / x ] ph <-> [ A / y ] ps ) ) $=
      ( vz vw cv wsbc wb wceq dfsbcq bibi12d cvv wcel visset ax-17 hbsbcg wi
      hbsbc1v bicomd eqcoms sbceq1a subtr2 mp2an sbciegf ax-mp vtoclg ) ACJLZMZ
      BDUMMZNZACEMZBDEMZNJEFUMEOUNUQUOURACUMEPBDUMEPQUMRSZUPJTZAUOCUMRBCDKUMRKL
      ZUMSZCUAHUBCLZRSUSVCUMOAUONUCCTUTBAUODKVCUMRRVAVCSDUAVBDUAGBDUMUTUDBANVCD
      LZVCVDOABIUEUFBDUMUGUHUIUJUKUL $.
      $( [19-Sep-2009] $)
  $}

  ${
    $d y z w A $.  $d y z w B $.  $d w C $.  $d w x y z $.
    $( Alternate definition of indexed intersection when ` B ` is a set.
       (Moved to ~ dfiin2g in main set.mm and may be deleted by mathbox owner,
       JGH. --NM 20-Sep-2011.) $)
    dfiin2gOLD $p |- ( A. x e. A B e. C -> |^|_ x e. A B = |^| { y | E. x e. A
        y =
    B } ) $=
      ( vw vz wcel wral cv cab wceq wrex wi wal ciin cint wb df-ral eleq2
      biimprcd 19.21aiv eqid eqeq1 imbi12d cla4gv mpii impbid2 imim2i pm5.74d
      alimi albi syl sylbi albii alcom 19.21v 3bitr2ri r19.23v visset rexbidv
      elab imbi1i bitr4i bitri syl6bb syl5bb abbidv df-iin df-int 3eqtr4g ) DEH
      ZACIZFJZDHZACIZFKGJZBJZDLZACMZBKZHZVNVQHZNZGOZFKACDPWAQVMVPWEFVMAJCHZVONZ
      AOZWEVPVMWHWFVQDLZWCNZGOZNZAOZWEVMWFVLNZAOZWHWMRZVLACSWOWGWLRZAOWPWNWQAWN
      WFVOWKVLVOWKRWFVLVOWKVOWJGWIWCVOVQDVNTZUAUBVLWKDDLZVODUCWJWSVONGDEWIWIWSW
      CVOVQDDUDWRUEUFUGUHUIUJUKWGWLAULUMUNWMWJACIZGOZWEXAWFWJNZAOZGOXBGOZAOWMWT
      XCGWJACSUOXBAGUPXDWLAWFWJGUQUOURWTWDGWTWIACMZWCNWDWIWCACUSWBXEWCVTXEBVQGU
      TVRVQLVSWIACVRVQDUDVAVBVCVDUOVEVFVOACSVGVHAFCDVIFGWAVJVK $.
      $( [27-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d z B $.  $d z C $.
    cbviin.1 $e |- ( z e. B -> A. y z e. B ) $.
    cbviin.2 $e |- ( z e. C -> A. x z e. C ) $.
    cbviin.3 $e |- ( x = y -> B = C ) $.
    $( Change bound variables in an indexed intersection. $)
    cbviin $p |- |^|_ x e. A B = |^|_ y e. A C $=
      ( cv wcel wral cab ciin wceq eleq2d cbvral abbii df-iin 3eqtr4i ) CJZEKZA
      DLZCMUAFKZBDLZCMADENBDFNUCUECUBUDABDGHAJBJOEFUAIPQRACDESBCDFST $.
      $( [26-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d y z B $.  $d x z C $.
    cbviinv.1 $e |- ( x = y -> B = C ) $.
    $( Change bound variables in an indexed intersection. $)
    cbviinv $p |- |^|_ x e. A B = |^|_ y e. A C $=
      ( vz cv wcel ax-17 cbviin ) ABGCDEGHZDIBJLEIAJFK $.
      $( [26-Aug-2009] $)
  $}

  ${
    $d s t A $.  $d s t B $.  $d s t F $.
    $( An image under the converse of a restriction. $)
    cnvresima $p |- ( `' ( F |` A ) " B ) = ( ( `' F " B ) i^i A ) $=
      ( vt vs cres ccnv cima cin cv wcel cop wa wex visset elima3 opelres
      opelcnv anbi1i 3bitr4i anbi2i anass bitr4i exbii 19.41v bitri elin
      bitr2i 3bitri eqriv ) DCAFZGZBHZCGZBHZAIZDJZUMKEJZBKZURUQLZULKZMZENZUSUTU
      NKZMZENZUQAKZMZUQUPKZEUQULBDOZPVCVEVGMZENVHVBVKEVBUSVDVGMZMVKVAVLUSUQURLZ
      UKKVMCKZVGMVAVLUQURCAEOZQURUQUKVOVJRVDVNVGURUQCVOVJRSTUAUSVDVGUBUCUDVEVGE
      UEUFVIUQUOKZVGMVHUQUOAUGVPVFVGEUQUNBVJPSUHUIUJ $.
      $( [12-Jul-2009] $)
  $}

  $( Dominance is trichotomous in the restricted case of ordinal numbers.
     (Moved to ~ domtriord in main set.mm and may be deleted by mathbox owner,
     JGH. --NM 26-Aug-2011.) $)
  dmsdtriordOLD $p |- ( ( A e. On /\ B e. On ) -> ( A ~<_ B <-> -. B ~< A ) )
      $=
    ( con0 wcel wa cdom wbr csdm wn cen wi sbth expcom a1i iman brsdom notbii
    bicomi bitri syl6ib wss onelss ssdomg sylan9r con3d wb word ordtri1 eloni
    syl2an ancoms sylibrd adantl syld ensymg adantr endom syl6 jcad syl6ibr
    con1d impbid ) ACDZBCDZEZABFGZBAHGZIZVEVFBAFGZBAJGZKZVHVFVKKVEVIVFVJBALMNVK
    VIVJIZEZIZVHVIVJOVHVNVGVMBAPZQRSTVEVFVGVEVFIZVMVGVEVPVIVLVEVPBAUAZVIVEVPABD
    ZIZVQVEVRVFVDVRABUAVCVFBAUBABCUCUDUEVDVCVQVSUFZBUGAUGVTVDVCBAUHBUIAUIUJUKUL
    VDVQVIKVCBACUCUMUNVEVJVFVEVJABJGZVFVCVJWAKVDBACUOUPABUQURUEUSVOUTVAVB $.
    $( [24-Oct-2009] $)

  ${
    $d a b c r s t R $.
    $( A relation intersected with its converse is an equivalence relation if
       the relation is transitive. $)
    trer $p |- ( A. a A. b A. c ( ( a R b /\ b R c ) -> a R c ) -> Er ( R i^i
        `'
  R ) ) $=
      ( vr vs vt cv wbr wa wi wal ccnv cin wer wceq breq1 anbi1d imbi12d
      2albidv a4v breq2 anbi12d imbi1d albidv anbi2d pm3.3 com23 adantrd imp3a
      syl 3syl adantld jcad brin visset brcnv anbi2i bitr2i syl6ib bitri
      anbi12i syl5ib ancom bicomi 3bitr4i biimpi jctil 19.21aiv 19.21aivv
      dfer2 sylibr ) BHZCHZAIZVNDHZAIZJZVMVPAIZKZDLCLZBLZEHZFHZAAMZNZIZWDWCWFIZ
      KZWGWDGHZWFIZJZWCWJWFIZKZJZGLZFLELWFOWBWPEFWBWOGWBWNWIWBWCWDAIZWDWCAIZJZW
      DWJAIZWJWDAIZJZJZWMWLWBXCWCWJAIZWJWCAIZJZWMWBXCXDXEWBWCVNAIZVQJZWCVPAIZKZ
      DLZCLZWQWDVPAIZJZXIKZDLZXCXDKZWAXLBEVMWCPZVTXJCDXRVRXHVSXIXRVOXGVQVMWCVNA
      QRVMWCVPAQSTUAXKXPCFVNWDPZXJXODXSXHXNXIXSXGWQVQXMVNWDWCAUBVNWDVPAQZUCUDUE
      UAXPWQWTJZXDKZXQXOYBDGVPWJPZXNYAXIXDYCXMWTWQVPWJWDAUBUFVPWJWCAUBSUAYBWSXB
      XDYBWQXBXDKWRYBXBWQXDYBWTWQXDKXAYBWQWTXDWQWTXDUGUHUIUHUIUJUKULWBWJVNAIZVQ
      JZWJVPAIZKZDLZCLZXAXMJZYFKZDLZXCXEKZWAYIBGVMWJPZVTYGCDYNVRYEVSYFYNVOYDVQV
      MWJVNAQRVMWJVPAQSTUAYHYLCFXSYGYKDXSYEYJYFXSYDXAVQXMVNWDWJAUBXTUCUDUEUAYLX
      AWRJZXEKZYMYKYPDEVPWCPZYJYOYFXEYQXMWRXAVPWCWDAUBUFVPWCWJAUBSUAYPWSXBXEYPW
      RXBXEKWQYPXBWRXEYPXAWRXEKWTXAWRXEUGUMUHUMUJUKULUNWMXDWCWJWEIZJXFWCWJAWEUO
      YRXEXDWCWJAEUPZGUPZUQURUSUTWGWSWKXBWGWQWCWDWEIZJZWSWCWDAWEUOZUUAWRWQWCWDA
      YSFUPZUQZURVAWKWTWDWJWEIZJXBWDWJAWEUOUUFXAWTWDWJAUUDYTUQURVAVBVCWGWHUUBWR
      WDWCWEIZJZWGWHUUBUUAWQJUUHWQUUAVDUUAWRWQUUGUUEUUGWQWDWCAUUDYSUQVEVBVAUUCW
      DWCAWEUOVFVGVHVIVJEFGWFVKVL $.
      $( [6-Oct-2009] $)
  $}

  $( An equivalent membership condition for closed intervals. $)
  elicc3 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,] B ) <-> ( C e. RR*
  /\ A <_ B /\ ( C = A \/ ( A < C /\ C < B ) \/ C = B ) ) ) ) $=
    ( cxr wcel wa cicc co cle wbr w3a wceq clt w3o elicc1 wi simp1 a1i xrletr
    exp5o com23 imp5q wn wne xrleltne biimprd df-ne syl5ibr 3adant3r3 adantlr
    eqcom necon3bbii syl5ib 3exp com12 imp32 3adantr2 adantll anim12d ex wo
    df-or 3orass impexp imbi2i orcom 3bitr2i 3bitr4ri syl6ib 3jcad breq2
    xrleid adantr ad2antrr syl5cbir xrltle adantllr adantrd simpr 3jaod exp31
    3impd breq1 ancoms adantld ad2antlr impbid bitrd ) ADEZBDEZFZCABGHECDEZACIJ
    ZCBIJZKZWLABIJZCALZACMJZCBMJZFZCBLZNZKZABCOWKWOXCWKWOWLWPXBWOWLPWKWLWMWNQRW
    IWJWLWMWNWPWIWLWJWMWNWPPPWIWLWJWMWNWPACBSTUAUBWKWOWQUCZXAUCZFWTPZXBWKWOXFWK
    WOFXDWRXEWSWIWOXDWRPZWJWIWLWMXGWNWIWLWMKZCAUDZWRXDXHWRXIACUEUFCAUGUHUIUJWJW
    OXEWSPZWIWJWLWNXJWMWJWLWNXJWLWJWNXJPWLWJWNXJWLWJWNKZBCUDZWSXEXKWSXLCBUEUFXA
    BCCBUKULUMUNUOUPUQURUSUTWQWTXAVAZVAXDXMPZXBXFWQXMVBWQWTXAVCXFXDXEWTPZPXDXAW
    TVAZPXNXDXEWTVDXPXOXDXAWTVBVEXPXMXDXAWTVFVEVGVHVIVJWKXCWLWMWNXCWLPWKWLWPXBQ
    RWKWLWPXBWMWKWLWPXBWMPWKWLFZWPFZWQWMWTXAWQWMAAIJZXRCAAIVKWKXSWLWPWIXSWJAVLV
    MVNVOXRWRWMWSWIWLWPWRWMPZWJWIWLFXTWPACVPVMVQVRXAWMWPXRCBAIVKXQWPVSZVOVTWAWB
    WKWLWPXBWNWKWLWPXBWNPXRWQWNWTXAWQWNWPXRCABIWCYAVOXQWTWNPZWPWJWLYBWIWJWLFWSW
    NWRWLWJWSWNPCBVPWDWEURVMXAWNBBIJZXRCBBIWCXQYCWPWJYCWIWLBVLWFVMVOVTWAWBVJWGW
    H $.
    $( [14-Jul-2009] $)

  ${
    $d x A $.
    $( A closed interval with identical lower and upper bounds is a
       singleton. $)
    ccid $p |- ( A e. RR* -> ( A [,] A ) = { A } ) $=
      ( vx cxr wcel cicc co csn cv cle wbr w3a wb elicc1 anidms wceq wi wa clt
      wn xrlenlt ancoms xrlttri3 biimprd exp3a com23 sylbid ex 3impd eleq1a
      breq2 xrleid syl5cbir breq1 3jcad impbid elsn syl6bbr bitrd eqrdv ) ACDZB
      AAEFZAGZUTBHZVADZVCCDZAVCIJZVCAIJZKZVCVBDZUTVDVHLAAVCMNUTVHVCAOZVIUTVHVJU
      TVEVFVGVJUTVEVFVGVJPZPUTVEQZVFVCARJSZVKAVCTVLVGVMVJVLVGAVCRJSZVMVJPVEUTVG
      VNLVCATUAVLVMVNVJVLVMVNVJVEUTVMVNQZVJPVEUTQVJVOVCAUBUCUAUDUEUFUEUFUGUHUTV
      JVEVFVGACVCUIVJVFAAIJZUTVCAAIUJAUKZULVJVGVPUTVCAAIUMVQULUNUOBAUPUQURUS $.
      $( [13-Jul-2009] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( If the upper bound of one open interval is less than or equal to the
       lower bound of the other, the intervals are disjoint. $)
    ioodisj $p |- ( ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) )
     /\ B <_ C ) -> ( ( A (,) B ) i^i ( C (,) D ) ) = (/) ) $=
      ( vx cxr wcel wa cle wbr cv cioo co cin wex wn c0 wceq simpr clt wi w3a
      xrlttr wb xrltnle 3adant2 sylibd exp3a com23 3exp com3r imp5g ad2ant2lr
      adantr elioo1 3simpb syl6bi ad2antrr ad2antlr simp2 syl2and elin syl5ib
      19.23adv mt2d wal eq0 alnex bitr2i sylib ) AFGZBFGZHZCFGZDFGZHZHZBCIJZHZE
      KZABLMZCDLMZNZGZEOZPZWCQRZVSWEVRVQVRSVSWDVRPZEVSVTWAGZVTWBGZHWHWDVSVTFGZV
      TBTJZHZCVTTJZWHWIWJVQWMWNHWHUAZVRVLVNWOVKVOVLVNWKWLWNWHVNWKVLWLWNWHUAUAZV
      NWKVLWPVNWKVLUBZWNWLWHWQWNWLWHWQWNWLHCBTJZWHCVTBUCVNVLWRWHUDWKCBUEUFUGUHU
      IUJUKULUMUNVMWIWMUAVPVRVMWIWKAVTTJZWLUBWMABVTUOWKWSWLUPUQURVSWJWKWNVTDTJZ
      UBZWNVPWJXAUDVMVRCDVTUOUSWKWNWTUTUQVAVTWAWBVBVCVDVEWGWDPEVFWFEWCVGWDEVHVI
      VJ $.
      $( [13-Jul-2009] $)
  $}

  $( Euler's constant ` _e ` is greater than 0. $)
  epos $p |- 0 < _e $=
    ( cc0 c2 clt wbr ceu 2pos egt2OLD 0re 2re ere lttri mp2an ) ABCDBECDAECDFGA
    BEHIJKL $.
    $( [22-Nov-2008] $)

  $( Euler's constant ` _e ` is a positive real. $)
  epr $p |- _e e. RR+ $=
    ( ceu ere epos elrpii ) ABCD $.
    $( [22-Nov-2008] $)

  ${
    $d a k m n y ph $.  $d a k m n x ps $.  $d x y $.
    finminlem.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( A useful lemma about finite sets.  If a property holds for a finite set,
       it holds for a minimal set. $)
    finminlem $p |- ( E. x e. Fin ph -> E. x ( ph /\ A. y ( ( y C_ x /\ ps ) ->
    x = y ) ) ) $=
      ( vn vm va vk cfn wrex cv cen wbr wa wex com crab c0 wne cin wceq wss wi
      wal hbe1 wcel ax-17 hbrab hbne isfi w3a 19.8a anim2i 3impb breq2 anbi1d
      exbidv elrab sylibr ne0i syl 3exp r19.23aiv sylbi r19.23ai con0 cep wwe
      epweon ssrab2 omsson sstri wefrc mp3an12 hbin hbeq hban simprr wpss wo
      wn ra4e ssfi pssss sylan2 ex sylbir adantrr simprll simprlr simplrr
      visset breq1 anbi12d cla4ev syl11anc csdm adantr php3 cdom endomtr
      ad2antrr ad2antlr domentr ensym expcom ad2antll syld cvv ssdomg ax-mp
      syl5 domnsym con2i nsyli impr word wb nnord ad2antrl ordtri1 con2bid
      mpbird jca31 elin anbi1i bitri exp44 r19.23adv syl5ib com23 mpdd
      necon2bd imp31 pm2.21d eqcom biimpi a1i jaod sspss expr imp3a 19.21aiv
      jca eximd imp 3syl ) ACJKCLZFLZMNZAOZCPZFQRZSTZUUNGLZUAZSUBZGUUNKZADLZUUI
      UCZBOUUIUUTUBZUDZDUEZOZCPZAUUOCJCHUUNSUUMCFHQUULCUFHLZQUGCUHUIZUVGSUGCUHZ
      UJUUIJUGZUUIUUPMNZGQKZAUUOUDZGUUIUKZUVKUVMGQUUPQUGZUVKAUUOUVOUVKAULZUUPUU
      NUGZUUOUVPUVOUVKAOZCPZOZUVQUVOUVKAUVTUVRUVSUVOUVRCUMUNUOUUMUVSFUUPQUUJUUP
      UBZUULUVRCUWAUUKUVKAUUJUUPUUIMUPUQURUSZUTUUNUUPVAVBVCVDVEVFVGVHVIUUNVGUCU
      UOUUSVJUUNQVGUUMFQVKVLVMGVGUUNVNVOUURUVFGUUNUVQUVTUURUVFUDZUWBUVOUVSUWCUV
      OUURUVSUVFUVOUURUVSUVFUDUVOUUROZUVRUVECUVOUURCUVOCUHCHHUUQSCHUUNUUPUVHUVG
      UUPUGCUHVPUVIVQVRUWDUVRUVEUWDUVROZAUVDUWDUVKAVSUWEUVCDUWEUVABUVBUWEBUVAUV
      BUWDUVRBUVAUVBUDUWDUVRBOZOZUUTUUIVTZUUTUUIUBZWAUVBUVAUWGUWHUVBUWIUWGUWHUV
      BUVOUURUWFUWHWBZUVOUWFUURUWJUVOUWFUURUWJUDUVOUWFOZUWHUUQSUWKUWHUUTJUGZUUQ
      STZUVOUVRUWHUWLUDZBUVOUVKUWNAUVOUVKOZUVLUWNUVKGQWCZUVLUVJUWNUVNUVJUWHUWLU
      VJUVAUWLUWHUUIUUTWDUUTUUIWEWFWGWHVBWIWIUWKUWLUWHUWMUWKUUTILZMNZIQKUWHUWMU
      DZUWLUWKUWRUWSIQUWKUWQQUGZUWRUWHUWMUWKUWTUWROZUWHOZOZUWQUUQUGZUWMUXCUWTUU
      IUWQMNZAOZCPZOZUWQUUPUGZOZUXDUXCUWTUXGUXIUWKUWTUWRUWHWJUXCUWRBUXGUWKUWTUW
      RUWHWKUVOUVRBUXBWLUXFUWRBOCUUTDWMUVBUXEUWRABUUIUUTUWQMWNEWOWPWQUXCUXIUUPU
      WQUCZWBZUWKUXAUWHUXLUWKUXAOZUWHUUTUUIWRNZUXLUXMUVJUWHUXNUDUWKUVJUXAUVOUVR
      UVJBUVOUVKUVJAUWOUVLUVJUWPUVNUTWIWIWSUVJUWHUXNUUIUUTWTWGVBUXMUXKUUIUUTXAN
      ZUXNUXMUUPUWQXANZUXOUXKUXMUXPUUIUWQXANZUXOUWFUXPUXQUDZUVOUXAUVKUXRABUVKUX
      PUXQUUIUUPUWQXBWGXCXDUWRUXQUXOUDUWKUWTUXQUWRUXOUXQUWQUUTMNUXOUWRUUIUWQUUT
      XEUUTUWQIWMXFWFXGXHXIUUPXJUGUXKUXPUDGWMUUPUWQXJXKXLXMUXOUXNUUIUUTXNXOXPXI
      XQUXCUUPXRZUWQXRZUXIUXLXSUVOUXSUWFUXBUUPXTXCUXAUXTUWKUWHUWTUXTUWRUWQXTWSY
      AUXSUXTOUXKUXIUUPUWQYBYCWQYDYEUXDUWQUUNUGZUXIOUXJUWQUUNUUPYFUYAUXHUXIUUMU
      XGFUWQQUUJUWQUBZUULUXFCUYBUUKUXEAUUJUWQUUIMUPUQURUSYGYHUTUUQUWQVAVBYIYJIU
      UTUKYKYLYMYNWGYLYOYPUWIUVBUDUWGUWIUVBUUTUUIYQYRYSYTUUTUUIUUAYKUUBYLUUCUUD
      UUEWGUUFWGYLUUGVEVDUUH $.
      $( [4-Dec-2009] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y C $.
    $( Subset relationship for function ` fi ` .  (Moved to ~ fiss in main
       set.mm and may be deleted by mathbox owner, JGH. --NM 21-May-2011.) $)
    fissOLD $p |- ( ( B e. C /\ A C_ B ) -> ( fi ` A ) C_ ( fi ` B ) ) $=
      ( vy vx wcel wss wa cv cfn cint wceq w3a wex cab cfi cfv wi sstr2 com12
      adantl idd 3anim123d eximdv ss2abdv cvv ssexg ancoms fiv syl adantr
      3sstr4d ) BCFZABGZHZDIZAGZUPJFZEIUPKLZMZDNZEOZUPBGZURUSMZDNZEOZAPQZBPQZUO
      VAVEEUOUTVDDUOUQVCURURUSUSUNUQVCRUMUQUNVCUPABSTUAUOURUBUOUSUBUCUDUEUOAUFF
      ZVGVBLUNUMVIABCUGUHDEAUFUIUJUMVHVFLUNDEBCUIUKUL $.
      $( [7-Oct-2009] $)
  $}


  ${
    $d x y z u v A $.  $d x y z u v B $.  $d x y z u v C $.  $d x y z u v D $.
    $d x y z u v K $.
    $( A finite intersection of elements taken from a union of collections. $)
    elfiun $p |- ( ( B e. D /\ C e. K ) -> ( A e. ( fi ` ( B u. C ) ) <-> ( A
    e. ( fi ` B ) \/ A e. ( fi ` C ) \/ E. x e. ( fi ` B ) E. y e. ( fi ` C ) A
    = ( x i^i y ) ) ) ) $=
      ( vz vu vv wcel wa cun cfi cfv cv cin wceq wrex w3o cvv w3a elisset
      adantl simpll simplr 3jca wi 3anim1i 3expib visset inex1 eleq1 mpbiri
      a1i r19.23aivv 3jaoi impcom wss cfn cint wex wb sppfi unexg sylan2 3impb
      c0 uneq12 unidm syl6eq eqeq1d inteq eqeq2d imbi1d vprc int0 eleq1i mtbir
      biimpcd mtoi pm2.21d syl5cbi 3ad2ant1 com12 a1i14 sylbid com23 wn uneq2
      uncom un0 3eqtr3g adantr df-ss syl6bbr simprr eqidd sseq1 3anbi123d
      cla4ev syl111anc wne df-ne inss1 ssn0 mpan sylbir ad2antrr intex sylib
      simprl3 syl11anc mpbird syl5cbir 3mix2 syl6 exp43 simprl2 3mix1 inss2
      ssfi biimpri simp2 mpan2 ad2antlr simp3 ad2antrl biimpar intun ineq1
      ineq2 rcla42ev ex 3mix3 4cases indi eqeq1i bitri syl5ib 3impd 19.23adv
      ssun1 fiss syl 3adant1 sseld simp1 sylibd ssun2 3ad2ant2 3ad2ant3
      anbi12d simpl1 simpr1 unss12 unfi 3ad2antr2 3ad2antl2 ineq12 syl6eqr
      3ad2antr3 3ad2antl3 biimpa unex exp31 19.23advv eeanv syl5ibr r19.23advv
      3jaod impbid bitrd pm5.21nd ) DFKZEGKZLZCDEMZNOZKZCDNOZKZCENOZKZCAPZBPZQZ
      RZBUWCSAUWASZTZCUAKZUVOUVPUBZUVQUVTLUWKUVOUVPUVTUWKUVQCUVSUCUDUVOUVPUVTUE
      UVOUVPUVTUFUGUWJUVQUWLUWBUVQUWLUHUWDUWIUWBUVOUVPUWLUWBUWKUVOUVPCUWAUCUIUJ
      UWDUVOUVPUWLUWDUWKUVOUVPCUWCUCUIUJUWIUVOUVPUWLUWIUWKUVOUVPUWHUWKABUWAUWCU
      WHUWKUHUWEUWAKZUWFUWCKZLZUWHUWKUWGUAKUWEUWFAUKZULCUWGUAUMUNUOUPUIUJUQURUW
      LUVTHPZUVRUSZUWQUTKZCUWQVAZRZUBZHVBZUWJUWKUVOUVPUVTUXCVCZUWKUVRUAKZUXDUVQ
      HCUAUVRUAVDZDEFGVEZVFVGZUWLUXCUWJUWLUXBUWJHUWLUWRUWSUXAUWJUWLUWQDQZUWQEQZ
      MZUWQRZUWSUXAUWJUHZUHZUWRUXIVHRZUXJVHRZUWLUXLUXNUHUHUXOUXPLZUXLUWLUXNUXQU
      XLVHUWQRZUWLUXNUHZUXQUXKVHUWQUXQUXKVHVHMVHUXIVHUXJVHVIVHVJVKVLUXQUXRUWLUW
      SUXMUWLUXRUXMUWKUVOUXRUXMUHUVPUXRCVHVAZRZUWJUHUXMUWKUXRUYAUXAUWJUXRUXTUWT
      CVHUWQVMVNVOUWKUYAUWJUWKUYAUXTUAKZUYBUAUAKVPUXTUAUAVQVRVSUYAUWKUYBCUXTUAU
      MVTWAWBWCWDWEWFWGWHUXOUXPWIZLZUXLUWLUXNUYDUXLUWQEUSZUXSUYDUXLUXJUWQRUYEUY
      DUXKUXJUWQUXOUXKUXJRUYCUXOUXJUXIMUXJVHMUXKUXJUXIVHUXJWJUXJUXIWKUXJWLWMWNV
      LUWQEWOWPUYCUYEUXSUHUXOUYCUYEUWLUWSUXMUYCUYELZUWLUWSLZLZUXAUWDUWJUXAUWDUW
      TUWCKZUYHCUWTUWCUMUYHUYIUWEEUSZUWEUTKZUWTUWEVAZRZUBZAVBZUYHUYEUWSUWTUWTRZ
      UYOUYCUYEUYGUFUYFUWLUWSWQUYHUWTWRUYNUYEUWSUYPUBAUWQHUKZUWEUWQRZUYJUYEUYKU
      WSUYMUYPUWEUWQEWSUWEUWQUTUMZUYRUYLUWTUWTUWEUWQVMVNZWTXAXBUYHUWTUAKZUVPUYI
      UYOVCUYHUWQVHXCZVUAUYCVUBUYEUYGUYCUXJVHXCZVUBUXJVHXDZUXJUWQUSZVUCVUBUWQEX
      EZUXJUWQXFXGXHXIUWQXJZXKUWKUVOUVPUWSUYFXLAUWTUAEGVDXMXNXOUWDUWBUWIXPXQXRU
      DWGWHUXOWIZUXPLZUXLUWLUXNVUIUXLUWQDUSZUXSVUIUXLUXIUWQRVUJVUIUXKUXIUWQUXPU
      XKUXIRVUHUXPUXKUXIVHMUXIUXJVHUXIWJUXIWLVKUDVLUWQDWOWPVUHVUJUXSUHUXPVUHVUJ
      UWLUWSUXMVUHVUJLZUYGLZUXAUWBUWJUXAUWBUWTUWAKZVULCUWTUWAUMVULVUMUWEDUSZUYK
      UYMUBZAVBZVULVUJUWSUYPVUPVUHVUJUYGUFVUKUWLUWSWQVULUWTWRVUOVUJUWSUYPUBAUWQ
      UYQUYRVUNVUJUYKUWSUYMUYPUWEUWQDWSUYSUYTWTXAXBVULVUAUVOVUMVUPVCVULVUBVUAVU
      HVUBVUJUYGVUHUXIVHXCZVUBUXIVHXDZUXIUWQUSZVUQVUBUWQDXEZUXIUWQXFXGXHXIVUGXK
      UWKUVOUVPUWSVUKXSAUWTUADFVDXMXNXOUWBUWDUWIXTXQXRWNWGWHVUHUYCLZUWLUXLUWSUX
      MVVAUWLLZUXLUWSLZLZUXAUWIUWJVVDUXAUWIVVDUXALZUXIVAZUWAKZUXJVAZUWCKZCVVFVV
      HQZRZUWIVVEVVGVUNUYKVVFUYLRZUBZAVBZVVEUXIDUSZUXIUTKZVVFVVFRZVVNVVOVVEUWQD
      YAUOVVDVVPUXAVVDUWSVUSVVPVVBUXLUWSWQVUSVVDVUTUOUWQUXIYBXMWNVVEVVFWRVVMVVO
      VVPVVQUBAUXIUWQDUYQULUWEUXIRZVUNVVOUYKVVPVVLVVQUWEUXIDWSUWEUXIUTUMVVRUYLV
      VFVVFUWEUXIVMVNWTXAXBVVEVVFUAKZUVOVVGVVNVCVVEVUQVVSVVBVUQVVCUXAVUHVUQUYCU
      WLVUQVUHVURYCXIXIUXIXJXKVVBUVOVVCUXAUWLUVOVVAUWKUVOUVPYDUDXIAVVFUADFVDXMX
      NVVEVVIUYJUYKVVHUYLRZUBZAVBZVVEUXJEUSZUXJUTKZVVHVVHRZVWBVWCVVEUWQEYAUOVVC
      VWDVVBUXAUWSVWDUXLUWSVUEVWDVUFUWQUXJYBYEUDYFVVEVVHWRVWAVWCVWDVWEUBAUXJUWQ
      EUYQULUWEUXJRZUYJVWCUYKVWDVVTVWEUWEUXJEWSUWEUXJUTUMVWFUYLVVHVVHUWEUXJVMVN
      WTXAXBVVEVVHUAKZUVPVVIVWBVCVVEVUCVWGVVBVUCVVCUXAUYCVUCVUHUWLVUCUYCVUDYCYF
      XIUXJXJXKVVBUVPVVCUXAUWLUVPVVAUWKUVOUVPYGUDXIAVVHUAEGVDXMXNVVECUXKVAZVVJV
      VDCVWHRZUXAUXLVWIUXAVCVVBUWSUXLVWHUWTCUXKUWQVMVNYHYIUXIUXJYJVKUWHVVKCVVFU
      WFQZRABVVFVVHUWAUWCUWEVVFRUWGVWJCUWEVVFUWFYKVNUWFVVHRVWJVVJCUWFVVHVVFYLVN
      YMXBYNUWIUWBUWDYOXQXRYPUWRUWQUVRQZUWQRUXLUWQUVRWOVWKUXKUWQUWQDEYQYRYSYTUU
      AUUBUWLUWBUXCUWDUWIUWLUWBUVTUXCUWLUWAUVSCUVOUVPUWAUVSUSZUWKUVQUXEVWLUXGUX
      EDUVRUSVWLDEUUCDUVRUAUUDYEUUEUUFUUGUWLUWKUXEUXDUWKUVOUVPUUHUVOUVPUXEUWKUX
      GUUFUXFXMUUIUWLUWDUVTUXCUWLUWCUVSCUVOUVPUWCUVSUSZUWKUVQUXEVWMUXGUXEEUVRUS
      VWMEDUUJEUVRUAUUDYEUUEUUFUUGUXHUUIUWLUWHUXCABUWAUWCUWLUWOIPZDUSZVWNUTKZUW
      EVWNVAZRZUBZIVBZJPZEUSZVXAUTKZUWFVXAVAZRZUBZJVBZLZUWHUXCUHZUWLUWMVWTUWNVX
      GUVOUWKUWMVWTVCZUVPUWEUAKUVOVXJUWPIUWEUADFVDXGUUKUVPUWKUWNVXGVCZUVOUWFUAK
      UVPVXKBUKJUWFUAEGVDXGUULUUMUWLVWSVXFLZJVBIVBVXIVXHUWLVXLVXIIJUWLVXLUWHUXC
      UWLVXLLZUWHLZVWNVXAMZUVRUSZVXOUTKZCVXOVAZRZUXCVXNVWOVXBVXPVXLVWOUWLUWHVWO
      VWPVWRVXFUUNYFVXLVXBUWLUWHVWSVXBVXCVXEUUOYFVWNDVXAEUUPXMVXLVXQUWLUWHVWPVW
      OVXFVXQVWRVWPVXBVXCVXQVXEVWNVXAUUQUURUUSYFVXMUWHVXSVXMUWGVXRCVXLUWGVXRRZU
      WLVWRVWOVXFVXTVWPVWRVXBVXEVXTVXCVWRVXELUWGVWQVXDQVXRUWEVWQUWFVXDUUTVWNVXA
      YJUVAUVBUVCUDVNUVDUXBVXPVXQVXSUBHVXOVWNVXAIUKJUKUVEUWQVXORZUWRVXPUWSVXQUX
      AVXSUWQVXOUVRWSUWQVXOUTUMVYAUWTVXRCUWQVXOVMVNWTXAXBUVFUVGVWSVXFIJUVHUVIWG
      UVJUVKUVLUVMUVN $.
      $( [15-Nov-2009] $)
  $}

  ${
    $d a b c k m n r s t u v w x y z A $.
    $d a b c k m n r s t u v w x y z B $.
    $( Lemma for ~ fictb . $)
    fictblem $p |- ( ( ( A e. B /\ A ~<_ om ) /\ ( m e. om /\ n e. om ) ) -> (
    n C_ m -> ( ( r e. ( rec ( { <. u , v >. | v = { w | E. y e. u E. z e. u w
    = ( y i^i z ) } } , A ) ` m ) /\ s e. ( rec ( { <. u , v >. | v = { w | E.
    y e. u E. z e. u w = ( y i^i z ) } } , A ) ` n ) ) -> E. x e. om ( r i^i s
    ) e. ( rec ( { <. u , v >. | v = { w | E. y e. u E. z e. u w = ( y i^i z )
    } } , A ) ` x ) ) ) ) $=
      ( vk vc vt va vb wcel com cdom wbr wa cv wss cin wceq wrex cab copab
      crdg cfv wi csuc fveq2 sseq2d ssid a1i id inidm eqcomi ineq1 eqeq2d
      ineq2 rcla42ev mpd3an23 visset eqeq1 2rexbidv elab sylibr ssriv con0
      nnon rdgsuc syl ad2antrr rexeq rexeqbi1dv abbidv sylan9bb cbvopabv
      cbvabv rexbidv cbvrexv rexbii bitri abbii eqtri eqeq2i opabbii fveq1i
      fvex abrexex abrexex2 fvopab syl6req sseqtrd sstr expcom findsg adantll
      sseld anim2d ex peano2 adantr ad2antlr eqid mp3an3 ad2antll inex1 eqtr3i
      syl6eleq eleqtrrd eleq2d rcla4ev syl11anc exp32 syldd ) GHRGSTUAUBZIUCZSR
      ZJUCZSRZUBZUBZYCYAUDZLUCZYAEUCZDUCZBUCZCUCZUEZUFZCFUCZUGZBYOUGZDUHZUFZFEU
      IZGUJZUKZRZKUCZYCUUAUKZRZUBZUUCUUDUUBRZUBZYHUUDUEZAUCZUUAUKZRZASUGZYFYGUU
      GUUIULYFYGUBZUUFUUHUUCUUOUUEUUBUUDYEYGUUEUUBUDZXTUUEUULUDUUEUUEUDZUUEMUCZ
      UUAUKZUDZUUEUURUMZUUAUKZUDZUUPAMYAYCUUKYCUFUULUUEUUEUUKYCUUAUNUOUUKUURUFU
      ULUUSUUEUUKUURUUAUNUOUUKUVAUFUULUVBUUEUUKUVAUUAUNUOUUKYAUFUULUUBUUEUUKYAU
      UAUNUOUUQYDUUEUPUQUURSRZYDUBYCUURUDZUBZUUSUVBUDZUUTUVCULUVFUUSNUCZYAYCUEZ
      UFZJUUSUGZIUUSUGZNUHZUVBUUSUVMUDUVFOUUSUVMOUCZUUSRZUVNUVIUFZJUUSUGIUUSUGZ
      UVNUVMRUVOUVOUVNUVNUVNUEZUFZUVQUVOURUVSUVOUVRUVNUVNUSUTUQUVPUVSUVNUVNYCUE
      ZUFIJUVNUVNUUSUUSYAUVNUFUVIUVTUVNYAUVNYCVAVBYCUVNUFUVTUVRUVNYCUVNUVNVCVBV
      DVEUVLUVQNUVNOVFUVHUVNUFUVJUVPIJUUSUUSUVHUVNUVIVGVHVIVJVKUQUVFUVBUUSYTUKZ
      UVMUVDUVBUWAUFZYDUVEUVDUURVLRUWBUURVMGUURYTVNVOVPUWAUUSUUDUVJJYHUGZIYHUGZ
      NUHZUFZLKUIZUKUVMUUSYTUWGYTUUDYNCYHUGZBYHUGZDUHZUFZLKUIUWGYSUWKFELKYOYHUF
      ZYSYIUWJUFYIUUDUFUWKUWLYRUWJYIUWLYQUWIDYPUWHBYOYHYNCYOYHVQVRVSVBYIUUDUWJV
      GVTWAUWKUWFLKUWJUWEUUDUWJUVHYMUFZCYHUGZBYHUGZNUHUWEUWIUWODNYJUVHUFYNUWMBC
      YHYHYJUVHYMVGVHWBUWOUWDNUWOUVHYAYLUEZUFZCYHUGZIYHUGUWDUWNUWRBIYHYKYAUFZUW
      MUWQCYHUWSYMUWPUVHYKYAYLVAVBWCWDUWRUWCIYHUWQUVJCJYHYLYCUFUWPUVIUVHYLYCYAV
      CVBWDWEWFWGWHWIWJWHWKLKUUSUWEUVMUURUUAWLZUVKINUUSUWTJNUUSUVIUWTWMWNYHUUSU
      FUWDUVLNUWCUVKIYHUUSUVJJYHUUSVQVRVSWOWHWPWQUUTUVGUVCUUEUUSUVBWRWSVOWTXAXB
      XCXDYFYGUUIUUNYFYGUUIUBZUBZYAUMZSRZUUJUXCUUAUKZRZUUNYEUXDXTUXAYBUXDYDYAXE
      XFXGUXBUUJUUBYTUKZUXEUXBUUJUVHPUCZQUCZUEZUFZQUUBUGZPUUBUGZNUHZUXGUXBUUJUX
      JUFZQUUBUGPUUBUGZUUJUXNRUUIUXPYFYGUUCUUHUUJUUJUFZUXPUUJXHUXOUXQUUJYHUXIUE
      ZUFPQYHUUDUUBUUBUXHYHUFUXJUXRUUJUXHYHUXIVAVBUXIUUDUFUXRUUJUUJUXIUUDYHVCVB
      VDXIXJUXMUXPNUUJYHUUDLVFXKUVHUUJUFUXKUXOPQUUBUUBUVHUUJUXJVGVHVIVJUUBUVNUX
      KQUURUGZPUURUGZNUHZUFZMOUIZUKUXNUXGMOUUBUYAUXNYAUUAWLZUXLPNUUBUYDQNUUBUXJ
      UYDWMWNUURUUBUFUXTUXMNUXSUXLPUURUUBUXKQUURUUBVQVRVSWOUUBUYCYTUYCUVNYNCUUR
      UGZBUURUGZDUHZUFZMOUIYTUYBUYHMOUYAUYGUVNUYAUWMCUURUGZBUURUGZNUHUYGUXTUYJN
      UXTUVHUXHYLUEZUFZCUURUGZPUURUGUYJUXSUYMPUURUXKUYLQCUURUXIYLUFUXJUYKUVHUXI
      YLUXHVCVBWDWEUYMUYIPBUURUXHYKUFZUYLUWMCUURUYNUYKYMUVHUXHYKYLVAVBWCWDWFWGU
      YJUYFNDUVHYJUFUWMYNBCUURUURUVHYJYMVGVHWBWHWIWJUYHYSMOFEUURYOUFZUYHUVNYRUF
      UVNYIUFYSUYOUYGYRUVNUYOUYFYQDUYEYPBUURYOYNCUURYOVQVRVSVBUVNYIYRVGVTWAWHWK
      XLXMYEUXEUXGUFZXTUXAYBUYPYDYBYAVLRUYPYAVMGYAYTVNVOXFXGXNUUMUXFAUXCSUUKUXC
      UFUULUXEUUJUUKUXCUUAUNXOXPXQXRXS $.
      $( [25-Aug-2009] $)

    $( A set is countable iff its collection of finite intersections is
       countable. $)
    fictb $p |- ( A e. B -> ( A ~<_ om <-> ( fi ` A ) ~<_ om ) ) $=
      ( vx vv vw vy vz vu vt vr vs vc vm vn vk va vb wcel com cdom wbr cfi cfv
      wa cv cin wceq wrex cab copab crdg ciun wss wral cint cfn w3a wex fiv
      sseq1 anbi1d abbidv inteqd sseq2 3anbi1d exbidv eqeq12d visset abfii5
      vtoclg eqtr4d adantr cvv fvex syl6eqelr omex iunex eleq2 raleqbi1dv
      anbi12d elab c0 rdg0g peano1 fveq2 ssiun2s ax-mp a1i eqsstr3d wi wo word
      ordtri2or2 nnord syl2an adantl fictblem incom eleq1i rexbii syl8ib ancom
      syl7ib ancom2s jaod mpd ex r19.23advv reeanv syl5ibr eleq2d cbvrexv
      anbi12i syl5ib eliun 3imtr4g r19.21aivv sylanbrc intss1 syl ssdomg sylc
      eqbrtrd domrefg csuc breq1d imbi2d simpr con0 nnon rdgsuc rexeq
      rexeqbi1dv eqeq2d eqeq1 sylan9bb cbvopabv 2rexbidv cbvabv ineq1 rexbidv
      ineq2 bitri abbii eqtri eqeq2i opabbii fveq1i abrexex abrexex2 fvopab
      syl6eq cxp copab2 wfo wfn crn co simplr inex1 eqid oprabval2g syl111anc
      eqeq1d eqcom syl6bb rexbidva wb fnoprab2 oprvelrn syl5bb abbi2dv jctil
      df-fo sylibr xpex fodom cen xpdom1 xpdom2 domtr syl11anc ad2antll xpomen
      domentr exp32 a2d finds com12 r19.21aiv iunctb abfi2 impbid ) ABRZASTUAZA
      UBUCZSTUAZUWNUWOUWQUWNUWOUDZUWPCSCUEZDUEZEUEZFUEZGUEZUFZUGZGHUEZUHZFUXFUH
      ZEUIZUGZHDUJZAUKZUCZULZTUAUXNSTUAZUWQUWRUWPAIUEZUMZJUEZKUEZUFZUXPRZKUXPUN
      ZJUXPUNZUDZIUIZUOZUXNTUWNUWPUYFUGUWOUWNUWPUXRAUMZUXRUPRZUXPUXRUOUGZUQZJUR
      ZIUIZUYFJIABUSLUEZUXPUMZUYCUDZIUIZUOZUXRUYMUMZUYHUYIUQZJURZIUIZUGUYFUYLUG
      LABUYMAUGZUYQUYFVUAUYLVUBUYPUYEVUBUYOUYDIVUBUYNUXQUYCUYMAUXPUTVAVBVCVUBUY
      TUYKIVUBUYSUYJJVUBUYRUYGUYHUYIUYMAUXRVDVEVFVBVGIJKUYMLVHVIVJVKVLZUWRUYFVM
      RUYFUXNUMZUYFUXNTUAUWRUYFUWPVMVUCAUBVNVOUWRUXNUYERZVUDVUEAUXNUMZUXTUXNRZK
      UXNUNZJUXNUNZUWRUYDVUFVUIUDIUXNCSUXMVPUWSUXLVNZVQUXPUXNUGUXQVUFUYCVUIUXPU
      XNAVDUYBVUHJUXPUXNUYAVUGKUXPUXNUXPUXNUXTVRVSVSVTWAUWRAWBUXLUCZUXNUWNVUKAU
      GUWOABUXKWCVLZVUKUXNUMZUWRWBSRVUMWDCSUXMWBVUKUWSWBUXLWEWFWGWHWIUWRVUGJKUX
      NUXNUWRUXRUXMRZCSUHZUXSUXMRZCSUHZUDZUXTUXMRZCSUHZUXRUXNRZUXSUXNRZUDVUGUWR
      UXRMUEZUXLUCZRZMSUHZUXSNUEZUXLUCZRZNSUHZUDZVUTVURUWRVVEVVIUDZNSUHMSUHVUTV
      VKUWRVVLVUTMNSSUWRVVCSRZVVGSRZUDZVVLVUTWJZUWRVVOUDZVVCVVGUMZVVGVVCUMZWKZV
      VPVVOVVTUWRVVCWLVVGWLVVTVVMVVNVVCVVGWMVVCWNVVGWNWOWPVVQVVRVVPVVSUWRVVNVVM
      VVRVVPWJUWRVVNVVMUDUDZVVRVVIVVEUDZVUTVVLVWAVVRVWBUXSUXRUFZUXMRZCSUHVUTCFG
      EDHABNMJKWQVWDVUSCSVWCUXTUXMUXSUXRWRWSWTXAVVEVVIXBXCXDCFGEDHABMNKJWQXEXFX
      GXHVVEVVIMNSSXIXJVUOVVFVUQVVJVUNVVECMSUWSVVCUGUXMVVDUXRUWSVVCUXLWEXKXLVUP
      VVICNSUWSVVGUGUXMVVHUXSUWSVVGUXLWEXKXLXMXNVVAVUOVVBVUQCUXRSUXMXOCUXSSUXMX
      OXMCUXTSUXMXOXPXQXRUXNUYEXSXTUYFUXNVMYAYBYCUWRSSTUAZUXMSTUAZCSUNUXOVWEUWR
      SVMRVWEVPSVMYDWGWHUWRVWFCSUWSSRUWRVWFUWRUXPUXLUCZSTUAZWJUWRVUKSTUAZWJUWRO
      UEZUXLUCZSTUAZWJUWRVWJYEZUXLUCZSTUAZWJUWRVWFWJIOUWSUXPWBUGZVWHVWIUWRVWPVW
      GVUKSTUXPWBUXLWEYFYGUXPVWJUGZVWHVWLUWRVWQVWGVWKSTUXPVWJUXLWEYFYGUXPVWMUGZ
      VWHVWOUWRVWRVWGVWNSTUXPVWMUXLWEYFYGUXPUWSUGZVWHVWFUWRVWSVWGUXMSTUXPUWSUXL
      WEYFYGUWRVUKASTVULUWNUWOYHYCVWJSRZUWRVWLVWOVWTUWRVWLVWOVWTUWRVWLUDZUDZVWN
      UYMVVCVVGUFZUGZNVWKUHZMVWKUHZLUIZSTVXBVWNVWKUXKUCZVXGVWTVWNVXHUGZVXAVWTVW
      JYIRVXIVWJYJAVWJUXKYKXTVLVXHVWKUXSVXDNUXRUHZMUXRUHZLUIZUGZJKUJZUCVXGVWKUX
      KVXNUXKUXSUXEGUXRUHZFUXRUHZEUIZUGZJKUJVXNUXJVXRHDJKUXFUXRUGZUXJUWTVXQUGUW
      TUXSUGVXRVXSUXIVXQUWTVXSUXHVXPEUXGVXOFUXFUXRUXEGUXFUXRYLYMVBYNUWTUXSVXQYO
      YPYQVXRVXMJKVXQVXLUXSVXQUYMUXDUGZGUXRUHZFUXRUHZLUIVXLVXPVYBELUXAUYMUGUXEV
      XTFGUXRUXRUXAUYMUXDYOYRYSVYBVXKLVYBUYMVVCUXCUFZUGZGUXRUHZMUXRUHVXKVYAVYEF
      MUXRUXBVVCUGZVXTVYDGUXRVYFUXDVYCUYMUXBVVCUXCYTYNUUAXLVYEVXJMUXRVYDVXDGNUX
      RUXCVVGUGVYCVXCUYMUXCVVGVVCUUBYNXLWTUUCUUDUUEUUFUUGUUEUUHJKVWKVXLVXGVWJUX
      LVNZVXEMLVWKVYGNLVWKVXCVYGUUIUUJUXRVWKUGVXKVXFLVXJVXEMUXRVWKVXDNUXRVWKYLY
      MVBUUKUUEUULVXBVXGVWKVWKUUMZTUAZVYHSTUAZVXGSTUAVXBVYHVXGPUEZVWKRQUEZVWKRU
      DUXPVYKVYLUFZUGUDPQIUUNZUUOZVYIVXBVYNVYHUUPZVYNUUQZVXGUGZUDVYOVXBVYRVYPVX
      BVXFLVYQVXBVVCVVGVYNUURZUYMUGZNVWKUHZMVWKUHZVXFUYMVYQRZVXBWUAVXEMVWKVXBVV
      CVWKRZUDZVYTVXDNVWKWUEVVGVWKRZUDZVYTVXCUYMUGVXDWUGVYSVXCUYMWUGWUDWUFVXCVM
      RZVYSVXCUGVXBWUDWUFUUSWUEWUFYHWUHWUGVVCVVGMVHUUTWHPQIVVCVVGVWKVWKVYMVXCVY
      NVVCVYLUFVMVYKVVCVYLYTVYLVVGVVCUUBVYNUVAZUVBUVCUVDVXCUYMUVEUVFUVGUVGVYPWU
      CWUBUVHPQIVWKVWKVYMVYNVYKVYLPVHUUTWUIUVIZMNVWKVWKUYMVYNUVJWGUVKUVLWUJUVMV
      YHVXGVYNUVNUVOVYHVXGVYNVWKVWKVYGVYGUVPUVQXTVXBVYHSSUUMZTUAZWUKSUVRUAZVYJV
      WLWULVWTUWRVWLVYHSVWKUUMZTUAWUNWUKTUAWULVWKSVWKVPVYGUVSVWKSSVPVPUVTVYHWUN
      WUKUWAUWBUWCWUMVXBUWDWHVYHWUKSUWEUWBVXGVYHSUWAUWBYCUWFUWGUWHUWIUWJCSUXMVP
      VUJUWKUWBUWPUXNSUWAUWBXGUWNAUWPTUAZUWQUWOWJUWNAUWPUMWUOABUWLAUWPBYAXFWUOU
      WQUWOAUWPSUWAXGXTUWM $.
      $( [24-Aug-2009] $)
  $}

  ${
    $d x y z A $.  $d x y z B $.
    $( Alternate form of ~ inficl for use with Jeff Hankins's mathbox.  (Moved
       to ~ inficlALT in main set.mm and may be deleted by mathbox owner, JGH.
       --NM 21-May-2011.) $)
    inficlALTOLD $p |- ( A e. B -> ( A. x e. A A. y e. A ( x i^i y ) e. A <-> (
        fi
    ` A ) = A ) ) $=
      ( vz wcel cv wss c0 wne cfn w3a cint wi wal cfi cfv wceq cin wral wex
      cab wb wa ax-17 hba1 hban 19.8a cvv intex isset bitr2i sylib 3ad2ant3
      simprl3 ax-4 com12 3com23 3expa 3adantl3 impcom adantll eqeltrd exp32
      mpdi 19.23ad csn snssi snfi a1i visset intsn eqcomi 3jca adantl snex
      sseq1 eleq1 inteq eqeq2d 3anbi123d cla4ev syl ex adantr impbid 19.21adv
      hbe1 hbbi hbal eqeq1 3anbi3d exbidv bibi12d cla4gv sylbi 3ad2ant2 simp1
      simp3 eqidd pm2.27 3syl bi1 syl5 syld cbvexv bibi1i albii syl5ib com23
      19.21ad abeq1 syl6bbr fiv eqeq1d bitr4d fiint syl5bb ) CDFZAGZCHZXTIJZXTK
      FZLZXTMZCFZNZAOZCPQZCRZXTBGZSCFBCTACTXSYHYAYCYKYERZLZAUAZBUBZCRZYJXSYHYNY
      KCFZUCZBOZYPXSYHYSXSYHYRBXSYHYRXSYHUDZYNYQYTYMYQAXSYHAXSAUEZYGAUFUGYQAUEZ
      YTYMYBYQYLYAYBYCYLYLBUAZYBYLBUHYBYEUIFZUUCXTUJZBYEUKULUMUNYTYMYBYQYTYMYBU
      DZUDYKYECYAYCYLYBYTUOYHUUFYFXSUUFYHYFYAYCYBYHYFNZYLYAYCYBUUGYAYBYCUUGYHYD
      YFYGAUPUQURUSUTVAVBVCVDVEVFXSYQYNNYHXSYQYNXSYQUDYKVGZCHZUUHKFZYKUUHMZRZLZ
      YNYQUUMXSYQUUIUUJUULYKCVHUUJYQYKVIVJUULYQUUKYKYKBVKVLVMVJVNVOYMUUMAUUHYKV
      PXTUUHRZYAUUIYCUUJYLUULXTUUHCVQXTUUHKVRUUNYEUUKYKXTUUHVSVTWAWBWCWDWEWFWDW
      GXSYSYGAUUAYRABYNYQAYMAWHUUBWIWJXSYDYSYFXSYDYSYFNXSYDUDZEGZCHZUUPKFZYKUUP
      MZRZLZEUAZYQUCZBOZYFYSUUOUVDUUQUURYEUUSRZLZEUAZYFUCZYFYDUVDUVHNZXSYBYAUVI
      YCYBUUDUVIUUEUVCUVHBYEUIYLUVBUVGYQYFYLUVAUVFEYLUUTUVEUUQUURYKYEUUSWKWLWMY
      KYECVRWNWOWPWQVOUUOUVGYFNZYFUVHUUOYAYCYEYERZLZUVGUVJYFNUUOYAYCUVKYDYAXSYA
      YBYCWRVOYDYCXSYAYBYCWSVOUUOYEWTVNUVFUVLEXTAVKUUPXTRZUUQYAUURYCUVEUVKUUPXT
      CVQUUPXTKVRUVMUUSYEYEUUPXTVSVTWAWBUVGYFXAXBUVGYFXCXDXEYRUVCBYNUVBYQYMUVAA
      EXTUUPRZYAUUQYCUURYLUUTXTUUPCVQXTUUPKVRUVNYEUUSYKXTUUPVSVTWAXFXGXHXIWDXJX
      KWFYNBCXLXMXSYIYOCABCDXNXOXPABCXQXR $.
      $( [6-Oct-2009] $)
  $}

  ${
    $d b c f j k n t w x y z A $.  $d b c f j k n t w x y z B $.
    $( A finite subset of the union of a superset chain is a subset of some
       element of the chain.  A useful preliminary result for ~ alexsub and
       others. $)
    finsschain $p |- ( ( ( A =/= (/) /\ A. x e. A A. y e. A ( x C_ y \/ y C_ x
    ) ) /\ ( B e. Fin /\ B C_ U. A ) ) -> E. z e. A B C_ z ) $=
      ( vb vk vn vj vc vf vw vt c0 wne cv wss wo wral wa cfn wcel cuni wrex wi
      wceq sseq1 rexbidv imbi12d imbi2d cen wbr com isfi wal csuc breq2 imbi1d
      albidv en0 wex 0ss sseq2 rcla4ev mpan2 syl5bir 19.23adv n0 syl5ib a1dd
      adantrd sylbi ax-gen wf1o ccnv cima cres wf1 f1of1 sssucid f1ores syl
      ad2antlr visset f1oen cvv cnvex imaexg ax-mp ensym 3syl simprr crn
      imassrn a1i wfo f1ofo forn sseqtrd simprl sstrd cfv csn wf f1of sucid
      ffvelrn sseldd eluni2 sylib snssi reximi cun orbi12d rcla42v ad2ant2r
      ad2antrr simplr jca unss syl11anc exp58 simpll simplrr simpr simpllr
      adantr jaod syld com23 imp3a com14 imp42 wfn f1ofn adantl fnsnfv uneq2d
      df-suc imaeq2i imaundi eqtr2i syl6eq foima eqtrd sseq1d mpbid exp512
      r19.23adv mpd cbvrexv imim2d mpid breq1 cla4v syl5 exp43 f1ocnv sucex
      bren com25 19.21adv cbvalv finds 19.21bi r19.23aiv vtoclga com12 imp32 )
      DNOZAPZBPZQZUVLUVKQZRZBDSADSZTZEUAUBZEDUCZQZECPZQZCDUDZUVRUVQUVTUWCUEZUVQ
      FPZUVSQZUWEUWAQZCDUDZUEZUEZUVQUWDUEFEUAUWEEUFZUWIUWDUVQUWKUWFUVTUWHUWCUWE
      EUVSUGUWKUWGUWBCDUWEEUWAUGUHUIUJUWEUAUBUWEGPZUKULZGUMUDUWJGUWEUNUWMUWJGUM
      UWLUMUBUWMUWJUEZFUWEHPZUKULZUWJUEZFUOUWENUKULZUWJUEZFUOUWEIPZUKULZUWJUEZF
      UOZUWEUWTUPZUKULZUWJUEZFUOZUWNFUOHIUWLUWONUFZUWQUWSFUXHUWPUWRUWJUWONUWEUK
      UQURUSUWOUWTUFZUWQUXBFUXIUWPUXAUWJUWOUWTUWEUKUQURUSUWOUXDUFZUWQUXFFUXJUWP
      UXEUWJUWOUXDUWEUKUQURUSUWOUWLUFZUWQUWNFUXKUWPUWMUWJUWOUWLUWEUKUQURUSUWSFU
      WRUWENUFZUWJUWEUTUXLUVJUWIUVPUXLUVJUWHUWFUXLUVKDUBZAVAUWHUVJUXLUXMUWHAUXL
      UWHNUWAQZCDUDZUXMUXLUWGUXNCDUWENUWAUGUHUXMNUVKQZUXOUVKVBUXNUXPCUVKDUWAUVK
      NVCVDVEVFVGADVHVIVJVKVLVMUWTUMUBZJPZUWTUKULZUVQUXRUVSQZUXRUWAQZCDUDZUEZUE
      ZUEZJUOZUXGUXCUXQUYFUXFFUXQUWFUXEUVQUYFUWHUXQUXEUWFUVQUYFUWHUEZUEZUXQUWEU
      XDKPZVNZKVAUWFUYHUEZUXEUXQUYJUYKKUXQUXDUWEUYIVOZVNZUYKUYJUXQUYMUWFUVQUYGU
      XQUYMTZUWFUVQTZTZUYLUWTVPZUWTUKULZUVQUYQUVSQZUYQUWAQZCDUDZUEZUEZUEZUWHUYF
      UYPVUDUYRUWHUYPUWTUYQUYLUWTVQZVNZUWTUYQUKULUYRUYMVUFUXQUYOUYMUXDUWEUYLVRZ
      VUFUXDUWEUYLVSVUGUWTUXDQVUFUWTVTUXDUWEUWTUYLWAVEWBWCUWTUYQVUEIWDZWEUWTUYQ
      UYLWFUBUYQWFUBUYIKWDWGUYLUWTWFWHWIZWJWKUYPVUCUWHUYRUYPVUCUVQUWHUYNUWFUVQW
      LUYPVUBUWHUVQUYPVUBUYSUWHUYPUYQUWEUVSUYMUYQUWEQUXQUYOUYMUYQUYLWMZUWEUYQVU
      JQUYMUYLUWTWNWOUYMUXDUWEUYLWPZVUJUWEUFUXDUWEUYLWQZUXDUWEUYLWRWBWSWCUYNUWF
      UVQWTZXAUYPVUAUWHUYSUYPUYQLPZQZLDUDUWHVUAUYPVUOUWHLDUYPUWTUYLXBZXCZMPZQZM
      DUDZVUNDUBZVUOUWHUEUEZUYPVUPVURUBZMDUDZVUTUYPVUPUVSUBVVDUYPUWEUVSVUPVUMUY
      MVUPUWEUBZUXQUYOUYMUXDUWEUYLXDZVVEUXDUWEUYLXEVVFUWTUXDUBZVVEUWTVUHXFZUXDU
      WEUWTUYLXGVEWBWCXHMVUPDXIXJVVCVUSMDVUPVURXKXLWBUYPVUSVVBMDUYPVURDUBZVUSVV
      AVUOUWHUYPVVIVUSTZVVAVUOTZTZTZUYQVUQXMZUWAQZCDUDZUWHUYNUWFUVQVVLVVPVVLUWF
      UVQUYNVVPVVLUVQUWFUYNVVPUEZVVLUVJUVPUWFVVQUEZVVLUVPUVJVVRVVLUVPVURVUNQZVU
      NVURQZRZUVJVVRUEZVVIVVAUVPVWAUEVUSVUOUVOVWAVURUVLQZUVLVURQZRABVURVUNDDUVK
      VURUFUVMVWCUVNVWDUVKVURUVLUGUVKVURUVLVCXNUVLVUNUFVWCVVSVWDVVTUVLVUNVURVCU
      VLVUNVURUGXNXOXPVVLVVSVWBVVTVVLVVSUVJUWFUYNVVPVVLVVSTUVJUWFTUYNTZTZVVAVVN
      VUNQZVVPVVLVVAVVSVWEVVJVVAVUOWTXQVWFVUOVUQVUNQZTVWGVWFVUOVWHVVLVUOVVSVWEV
      VJVVAVUOWLXQVWFVUQVURVUNVVLVUSVVSVWEVVIVUSVVKXRXQVVLVVSVWEXRXAXSUYQVUQVUN
      XTXJVVOVWGCVUNDUWAVUNVVNVCVDYAYBVVLVVTUVJUWFUYNVVPVVLVVTTZVWETZVVIVVNVURQ
      ZVVPVVLVVIVVTVWEVVIVUSVVKYCXQVWJUYQVURQZVUSTZVWKVWIVWMVWEVWIVWLVUSVWIUYQV
      UNVURVVJVVAVUOVVTYDVVLVVTYEXAVVIVUSVVKVVTYFXSYGUYQVUQVURXTXJVVOVWKCVURDUW
      AVURVVNVCVDYAYBYHYIYJYKYJYLYMVVMVVOUWGCDVVMVVNUWEUWAVVMVVNUYLUXDVPZUWEVVM
      VVNUYQUYLUWTXCZVPZXMZVWNVVMVUQVWPUYQVVMUYLUXDYNZVVGVUQVWPUFUYNVWRUYOVVLUY
      MVWRUXQUXDUWEUYLYOYPXQVVGVVMVVHWOUXDUWTUYLYQYAYRVWNUYLUWTVWOXMZVPVWQUXDVW
      SUYLUWTYSYTUYLUWTVWOUUAUUBUUCUYNVWNUWEUFZUYOVVLUYMVWTUXQUYMVUKVWTVULUXDUW
      EUYLUUDWBYPXQUUEUUFUHUUGUUHUUIUUJUUIUYTVUOCLDUWAVUNUYQVCUUKVIUULUUMUULUUM
      UULUUMUYEVUDJUYQVUIUXRUYQUFZUXSUYRUYDVUCUXRUYQUWTUKUUNVXAUYCVUBUVQVXAUXTU
      YSUYBVUAUXRUYQUVSUGVXAUYAUYTCDUXRUYQUWAUGUHUIUJUIUUOUUPUUQUWEUXDUYIUURUUP
      VGUWEUXDKUWTVUHUUSUUTVIYJUVAUVBUXBUYEFJUWEUXRUFZUXAUXSUWJUYDUWEUXRUWTUKUU
      NVXBUWIUYCUVQVXBUWFUXTUWHUYBUWEUXRUVSUGVXBUWGUYACDUWEUXRUWAUGUHUIUJUIUVCV
      IUVDUVEUVFVLUVGUVHUVI $.
      $( [25-Jan-2010] $)
  $}

