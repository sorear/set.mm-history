$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                              Miscellany
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  ${
    a1i13.1 $e |- ( ps -> th ) $.
    $( Add two antecedents to a wff.  (Contributed by Jeff Hankins,
       4-Aug-2009.) $)
    a1i13 $p |- ( ph -> ( ps -> ( ch -> th ) ) ) $=
      ( wi a1d a1i ) BCDFFABDCEGH $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i4.1 $e |- ( ph -> ( ps -> ( ch -> ta ) ) ) $.
    $( Add an antecedent to a wff.  (Contributed by Jeff Hankins,
       4-Aug-2009.) $)
    a1i4 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi ax-1 syl8 ) ABCEDEGFEDHI $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i14.1 $e |- ( ps -> ( ch -> ta ) ) $.
    $( Add two antecedents to a wff.  (Contributed by Jeff Hankins,
       4-Aug-2009.) $)
    a1i14 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd a1i ) BCDEGGGABCEDFHI $.
      $( [4-Aug-2009] $)
  $}

  ${
    a1i24.1 $e |- ( ph -> ( ch -> ta ) ) $.
    $( Add two antecedents to a wff.  (Contributed by Jeff Hankins,
       5-Aug-2009.) $)
    a1i24 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd a1d ) ACDEGGBACEDFHI $.
      $( [5-Aug-2009] $)
  $}

  ${
    a1i34.1 $e |- ( ph -> ( ps -> ta ) ) $.
    $( Add two antecedents to a wff.  (Contributed by Jeff Hankins,
       5-Aug-2009.) $)
    a1i34 $p |- ( ph -> ( ps -> ( ch -> ( th -> ta ) ) ) ) $=
      ( wi a1dd ) ABDEGCABEDFHH $.
      $( [5-Aug-2009] $)
  $}

  ${
    imp5.1 $e |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $.
    $( An importation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    imp5g $p |- ( ( ph /\ ps ) -> ( ( ( ch /\ th ) /\ ta ) -> et ) ) $=
      ( wa wi imp imp4c ) ABHCDEFABCDEFIIIGJK $.
      $( [7-Jul-2009] $)

    $( An importation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    imp55 $p |- ( ( ( ph /\ ( ps /\ ( ch /\ th ) ) ) /\ ta ) -> et ) $=
      ( wa wi imp4a imp42 ) ABCDHEFABCDEFIGJK $.
      $( [7-Jul-2009] $)

    $( An importation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    imp511 $p |- ( ( ph /\ ( ( ps /\ ( ch /\ th ) ) /\ ta ) ) -> et ) $=
      ( wa wi imp4a imp44 ) ABCDHEFABCDEFIGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5d.1 $e |- ( ( ( ph /\ ps ) /\ ch ) -> ( ( th /\ ta ) -> et ) ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp5d $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp31 ) ABCDEFHHABICIDEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5g.1 $e |- ( ( ph /\ ps ) -> ( ( ( ch /\ th ) /\ ta ) -> et ) ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp5g $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp4c ex ) ABCDEFHHHABICDEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5j.1 $e |- ( ph -> ( ( ( ( ps /\ ch ) /\ th ) /\ ta ) -> et ) ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp5j $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp4c ) ABCDEFHABCIDIEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5k.1 $e |- ( ph -> ( ( ( ps /\ ( ch /\ th ) ) /\ ta ) -> et ) ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp5k $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wi wa exp3a exp4d ) ABCDEFHABCDIIEFGJK $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp5l.1 $e |- ( ph -> ( ( ( ps /\ ch ) /\ ( th /\ ta ) ) -> et ) ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp5l $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa exp3a exp5c ) ABCDEFABCHDEHFGIJ $.
      $( [27-Jan-2010] $) $( [7-Jul-2009] $)
  $}

  ${
    exp56.1 $e |- ( ( ( ( ph /\ ps ) /\ ch ) /\ ( th /\ ta ) ) -> et ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp56 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5d ) ABCDEFABHCHDEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp58.1 $e |- ( ( ( ph /\ ps ) /\ ( ( ch /\ th ) /\ ta ) ) -> et ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp58 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5g ) ABCDEFABHCDHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp510.1 $e |- ( ( ph /\ ( ( ( ps /\ ch ) /\ th ) /\ ta ) ) -> et ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp510 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5j ) ABCDEFABCHDHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp511.1 $e |- ( ( ph /\ ( ( ps /\ ( ch /\ th ) ) /\ ta ) ) -> et ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp511 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5k ) ABCDEFABCDHHEHFGIJ $.
      $( [7-Jul-2009] $)
  $}

  ${
    exp512.1 $e |- ( ( ph /\ ( ( ps /\ ch ) /\ ( th /\ ta ) ) ) -> et ) $.
    $( An exportation inference.  (Contributed by Jeff Hankins, 7-Jul-2009.) $)
    exp512 $p |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $=
      ( wa ex exp5l ) ABCDEFABCHDEHHFGIJ $.
      $( [27-Jan-2010] $) $( [7-Jul-2009] $)
  $}

  ${
    mtord.1 $e |- ( ph -> -. ch ) $.
    mtord.2 $e |- ( ph -> -. th ) $.
    mtord.3 $e |- ( ph -> ( ps -> ( ch \/ th ) ) ) $.
    $( A modus tollens deduction involving disjunction.  (Contributed by Jeff
       Hankins, 15-Jul-2009.) $)
    mtord $p |- ( ph -> -. ps ) $=
      ( wn wo wi df-or syl6ib mpid mtod ) ABDFABCHZDEABCDIODJGCDKLMN $.
      $( [15-Jul-2009] $)
  $}

  ${
    3com12d.1 $e |- ( ph -> ( ps /\ ch /\ th ) ) $.
    $( Commutation in consequent.  Swap 1st and 2nd.  (Contributed by Jeff
       Hankins, 17-Nov-2009.) $)
    3com12d $p |- ( ph -> ( ch /\ ps /\ th ) ) $=
      ( w3a id 3com12 syl ) ABCDFCBDFZECBDJJGHI $.
      $( [17-Nov-2009] $)
  $}

  ${
    3imp5.1 $e |- ( ph -> ( ps -> ( ch -> ( th -> ( ta -> et ) ) ) ) ) $.
    $( A triple importation inference.  (Contributed by Jeff Hankins,
       8-Jul-2009.) $)
    imp5p $p |- ( ph -> ( ps -> ( ( ch /\ th /\ ta ) -> et ) ) ) $=
      ( w3a wi com52l 3imp com3l ) CDEHABFCDEABFIIABCDEFGJKL $.
      $( [8-Jul-2009] $)

    $( A triple importation inference.  (Contributed by Jeff Hankins,
       8-Jul-2009.) $)
    imp5q $p |- ( ( ph /\ ps ) -> ( ( ch /\ th /\ ta ) -> et ) ) $=
      ( wa wi imp 3impd ) ABHCDEFABCDEFIIIGJK $.
      $( [8-Jul-2009] $)
  $}


  ${
    ecase13d.1 $e |- ( ph -> -. ch ) $.
    ecase13d.2 $e |- ( ph -> -. th ) $.
    ecase13d.3 $e |- ( ph -> ( ch \/ ps \/ th ) ) $.
    $( Deduction for elimination by cases.  (Contributed by Jeff Hankins,
       18-Aug-2009.) $)
    ecase13d $p |- ( ph -> ps ) $=
      ( wn wo wi w3o 3orass df-or bitri sylib mpd orcom ) ADHZBFABDIZRBJZACHZSE
      ACBDKZUASJZGUBCSIUCCBDLCSMNOPSDBITBDQDBMNOP $.
      $( [18-Aug-2009] $)
  $}

  ${
    $d y ph $.  $d x y ps $.  $d x y A $.
    eqeu.1 $e |- ( x = A -> ( ph <-> ps ) ) $.
    $( A condition which implies existential uniqueness.  (Contributed by Jeff
       Hankins, 8-Sep-2009.) $)
    eqeu $p |- ( ( A e. B /\ ps /\ A. x ( ph -> x = A ) ) -> E! x ph ) $=
      ( vy wcel cv wceq wi wal w3a wex weu cla4egv imp 3adant3 eqeq2 imbi2d
      albidv 3adant2 ax-17 eu3 sylanbrc ) DEHZBACIZDJZKZCLZMACNZAUGGIZJZKZCLZGN
      ZACOUFBUKUJUFBUKABCDEFPQRUFUJUPBUFUJUPUOUJGDEULDJZUNUICUQUMUHAULDUGSTUAPQ
      UBACGAGUCUDUE $.
      $( [8-Sep-2009] $)
  $}

  ${
    $d z w ph $.  $d z w ps $.  $d z w ch $.  $d y z w A $.  $d y z w B $.
    $d y z w X $.  $d y z w Y $.  $d y z w Z $.  $d x y z w $.
    subtr.1 $e |- ( y e. A -> A. x y e. A ) $.
    subtr.2 $e |- ( y e. B -> A. x y e. B ) $.
    ${
      subtr.3 $e |- ( y e. Y -> A. x y e. Y ) $.
      subtr.4 $e |- ( y e. Z -> A. x y e. Z ) $.
      subtr.5 $e |- ( x = A -> X = Y ) $.
      subtr.6 $e |- ( x = B -> X = Z ) $.
      $( Transitivity of implicit substitution.  (Contributed by Jeff Hankins,
         13-Sep-2009.) $)
      subtr $p |- ( ( A e. C /\ B e. D ) -> ( A = B -> Y = Z ) ) $=
        ( vz vw cv wceq csb wi eqeq1 wex a9e wcel ax-17 hbeq visset hbcsb1
        hbim csbeq1a sylan9req ex sylbird 19.23ai ax-mp eqeq1d imbi12d eqeq2
        eqeq2d csbeq1 vtocl2g ) PRZQRZSZAVCGTZAVDGTZSZUACVDSZHVGSZUACDSZHISZUAP
        QCDEFVCCSZVEVIVHVJVCCVDUBVMVFHVGARZVCSZAUCVMVFHSZUAZAPUDVOVQAVMVPAABBVC
        CBRZVCUEAUFZJUGABBVFHABVCGPUHVSUILUGUJVOVMVNCSZVPVNVCCUBVOVTVPVOVTVFGHA
        VCGUKNULUMUNUOUPUQURVDDSZVIVKVJVLVDDCUSWAVGIHVNVDSZAUCWAVGISZUAZAQUDWBW
        DAWAWCAABBVDDVRVDUEAUFZKUGABBVGIABVDGQUHWEUIMUGUJWBWAVNDSZWCVNVDDUBWBWF
        WCWBWFVGGIAVDGUKOULUMUNUOUPUTURAVCVDGVAVB $.
        $( [13-Sep-2009] $)
    $}

    ${
      subtr2.3 $e |- ( ps -> A. x ps ) $.
      subtr2.4 $e |- ( ch -> A. x ch ) $.
      subtr2.5 $e |- ( x = A -> ( ph <-> ps ) ) $.
      subtr2.6 $e |- ( x = B -> ( ph <-> ch ) ) $.
      $( Transitivity of implicit substitution into a wff.  (Contributed by
         Jeff Hankins, 19-Sep-2009.) $)
      subtr2 $p |- ( ( A e. C /\ B e. D ) -> ( A = B -> ( ps <-> ch ) ) ) $=
        ( vz vw cv wceq wsbc wb wi eqeq1 wex a9e wcel ax-17 hbeq visset
        hbsbc1v hbbi hbim sbceq1a bicomd sylan9bb ex sylbird 19.23ai ax-mp
        bibi1d imbi12d eqeq2 bibi2d dfsbcq vtocl2g ) PRZQRZSZADVFTZADVGTZUAZUBF
        VGSZBVJUAZUBFGSZBCUAZUBPQFGHIVFFSZVHVLVKVMVFFVGUCVPVIBVJDRZVFSZDUDVPVIB
        UAZUBZDPUEVRVTDVPVSDDEEVFFERZVFUFDUGJUHVIBDADVFPUIUJLUKULVRVPVQFSZVSVQV
        FFUCVRWBVSVRVIAWBBVRAVIADVFUMUNNUOUPUQURUSUTVAVGGSZVLVNVMVOVGGFVBWCVJCB
        VQVGSZDUDWCVJCUAZUBZDQUEWDWFDWCWEDDEEVGGWAVGUFDUGKUHVJCDADVGQUIUJMUKULW
        DWCVQGSZWEVQVGGUCWDWGWEWDVJAWGCWDAVJADVGUMUNOUOUPUQURUSVCVAADVFVGVDVE
        $.
        $( [19-Sep-2009] $)
    $}
  $}

  ${
    $d w A $.  $d z w C $.  $d z w D $.  $d z w x y $.
    cbvcsb.1 $e |- ( z e. C -> A. y z e. C ) $.
    cbvcsb.2 $e |- ( z e. D -> A. x z e. D ) $.
    cbvcsb.3 $e |- ( x = y -> C = D ) $.
    $( Change bound variables in a class substitution.  Interestingly, this
       does not require any bound variable conditions on ` A ` .  (Contributed
       by Jeff Hankins, 13-Sep-2009.) $)
    cbvcsb $p |- ( A e. B -> [_ A / x ]_ C = [_ A / y ]_ D ) $=
      ( vw cv csb wceq csbeq1 eqeq12d visset cvv wcel wal wi ax-17 hbcsbg
      ax-mp hbcsb1 eqcomd eqcoms csbeq1a subtr mp2an csbief vtoclg ) AKLZFMZBUM
      GMZNADFMZBDGMZNKDEUMDNUNUPUOUQAUMDFOBUMDGOPACUMFUOKQZUMRSZCLZUOSZVAATUAUR
      ABCUMGRUTUMSZAUBIUCUDALZRSUSVCUMNFUONUAAQURBCVCUMRRGFUOUTVCSBUBVBBUBZHBCU
      MGURVDUEGFNVCBLZVCVENFGJUFUGBUMGUHUIUJUKUL $.
      $( [13-Sep-2009] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.
    $( A subset of a difference does not intersect the subtrahend.
       (Contributed by Jeff Hankins, 1-Sep-2013.) $)
    ssdifin0 $p |- ( A C_ ( B \ C ) -> ( A i^i C ) = (/) ) $=
      ( vx cv wcel wn wa wi wal cdif wss cin c0 wceq simpr imim2i alimi dfss2
      eldif imbi2i albii bitri disj1 3imtr4i ) DEZAFZUFBFZUFCFGZHZIZDJZUGUIIZDJ
      ABCKZLZACMNOUKUMDUJUIUGUHUIPQRUOUGUFUNFZIZDJULDAUNSUQUKDUPUJUGUFBCTUAUBUC
      DACUDUE $.
      $( [1-Sep-2013] $)
  $}

  ${
    $d s t A $.  $d s t B $.  $d s t F $.
    $( An image under the converse of a restriction.  (Contributed by Jeff
       Hankins, 12-Jul-2009.) $)
    cnvresima $p |- ( `' ( F |` A ) " B ) = ( ( `' F " B ) i^i A ) $=
      ( vt vs cres ccnv cima cin cv wcel cop wa wex visset elima3 opelres
      opelcnv anbi1i 3bitr4i anbi2i anass bitr4i exbii 19.41v bitri elin
      bitr2i 3bitri eqriv ) DCAFZGZBHZCGZBHZAIZDJZUMKEJZBKZURUQLZULKZMZENZUSUTU
      NKZMZENZUQAKZMZUQUPKZEUQULBDOZPVCVEVGMZENVHVBVKEVBUSVDVGMZMVKVAVLUSUQURLZ
      UKKVMCKZVGMVAVLUQURCAEOZQURUQUKVOVJRVDVNVGURUQCVOVJRSTUAUSVDVGUBUCUDVEVGE
      UEUFVIUQUOKZVGMVHUQUOAUGVPVFVGEUQUNBVJPSUHUIUJ $.
      $( [12-Jul-2009] $)
  $}

  ${
    $d a b c r s t R $.
    $( A relation intersected with its converse is an equivalence relation if
       the relation is transitive.  (Contributed by Jeff Hankins,
       6-Oct-2009.) $)
    trer $p |- ( A. a A. b A. c ( ( a R b /\ b R c ) -> a R c ) -> Er ( R i^i
        `'
  R ) ) $=
      ( vr vs vt cv wbr wa wi wal ccnv cin wer brin visset brcnv anbi2i bitri
      anbi12i wceq breq1 anbi1d imbi12d 2albidv a4v breq2 anbi12d imbi1d
      albidv anbi2d pm3.3 com23 adantrd imp3a syl 3syl adantld jcad bitr2i
      syl6ib syl5bi ancom bicomi 3bitr4i biimpi jctil 19.21aiv 19.21aivv dfer2
      sylibr ) BHZCHZAIZVNDHZAIZJZVMVPAIZKZDLCLZBLZEHZFHZAAMZNZIZWDWCWFIZKZWGWD
      GHZWFIZJZWCWJWFIZKZJZGLZFLELWFOWBWPEFWBWOGWBWNWIWLWCWDAIZWDWCAIZJZWDWJAIZ
      WJWDAIZJZJZWBWMWGWSWKXBWGWQWCWDWEIZJZWSWCWDAWEPZXDWRWQWCWDAEQZFQZRZSTWKWT
      WDWJWEIZJXBWDWJAWEPXJXAWTWDWJAXHGQZRSTUAWBXCWCWJAIZWJWCAIZJZWMWBXCXLXMWBW
      CVNAIZVQJZWCVPAIZKZDLZCLZWQWDVPAIZJZXQKZDLZXCXLKZWAXTBEVMWCUBZVTXRCDYFVRX
      PVSXQYFVOXOVQVMWCVNAUCUDVMWCVPAUCUEUFUGXSYDCFVNWDUBZXRYCDYGXPYBXQYGXOWQVQ
      YAVNWDWCAUHVNWDVPAUCZUIUJUKUGYDWQWTJZXLKZYEYCYJDGVPWJUBZYBYIXQXLYKYAWTWQV
      PWJWDAUHULVPWJWCAUHUEUGYJWSXBXLYJWQXBXLKWRYJXBWQXLYJWTWQXLKXAYJWQWTXLWQWT
      XLUMUNUOUNUOUPUQURWBWJVNAIZVQJZWJVPAIZKZDLZCLZXAYAJZYNKZDLZXCXMKZWAYQBGVM
      WJUBZVTYOCDUUBVRYMVSYNUUBVOYLVQVMWJVNAUCUDVMWJVPAUCUEUFUGYPYTCFYGYOYSDYGY
      MYRYNYGYLXAVQYAVNWDWJAUHYHUIUJUKUGYTXAWRJZXMKZUUAYSUUDDEVPWCUBZYRUUCYNXMU
      UEYAWRXAVPWCWDAUHULVPWCWJAUHUEUGUUDWSXBXMUUDWRXBXMKWQUUDXBWRXMUUDXAWRXMKW
      TXAWRXMUMUSUNUSUPUQURUTWMXLWCWJWEIZJXNWCWJAWEPUUFXMXLWCWJAXGXKRSVAVBVCWGW
      HXEWRWDWCWEIZJZWGWHXEXDWQJUUHWQXDVDXDWRWQUUGXIUUGWQWDWCAXHXGRVEUATXFWDWCA
      WEPVFVGVHVIVJEFGWFVKVL $.
      $( [6-Oct-2009] $)
  $}

  $( An equivalent membership condition for closed intervals.  (Contributed by
     Jeff Hankins, 14-Jul-2009.) $)
  elicc3 $p |- ( ( A e. RR* /\ B e. RR* ) -> ( C e. ( A [,] B ) <-> ( C e. RR*
  /\ A <_ B /\ ( C = A \/ ( A < C /\ C < B ) \/ C = B ) ) ) ) $=
    ( cxr wcel wa cicc co cle wbr w3a wceq clt w3o elicc1 wi simp1 a1i xrletr
    exp5o com23 imp5q wn wne df-ne xrleltne biimprd syl5bir 3adant3r3 adantlr
    eqcom necon3bbii syl5bi 3exp com12 imp32 3adantr2 adantll anim12d ex wo
    df-or 3orass impexp imbi2i orcom 3bitr2i 3bitr4ri syl6ib 3jcad xrleid
    ad3antrrr breq2 syl5ibrcom xrltle adantr adantllr adantrd simpr 3jaod
    exp31 3impd breq1 ancoms adantld ad2antlr impbid bitrd ) ADEZBDEZFZCABGHECD
    EZACIJZCBIJZKZWLABIJZCALZACMJZCBMJZFZCBLZNZKZABCOWKWOXCWKWOWLWPXBWOWLPWKWLW
    MWNQRWIWJWLWMWNWPWIWLWJWMWNWPPPWIWLWJWMWNWPACBSTUAUBWKWOWQUCZXAUCZFWTPZXBWK
    WOXFWKWOFXDWRXEWSWIWOXDWRPZWJWIWLWMXGWNXDCAUDZWIWLWMKZWRCAUEXIWRXHACUFUGUHU
    IUJWJWOXEWSPZWIWJWLWNXJWMWJWLWNXJWLWJWNXJPWLWJWNXJXEBCUDZWLWJWNKZWSXABCCBUK
    ULXLWSXKCBUFUGUMUNUOUPUQURUSUTWQWTXAVAZVAXDXMPZXBXFWQXMVBWQWTXAVCXFXDXEWTPZ
    PXDXAWTVAZPXNXDXEWTVDXPXOXDXAWTVBVEXPXMXDXAWTVFVEVGVHVIVJWKXCWLWMWNXCWLPWKW
    LWPXBQRWKWLWPXBWMWKWLWPXBWMPWKWLFZWPFZWQWMWTXAXRWMWQAAIJZWIXSWJWLWPAVKVLCAA
    IVMVNXRWRWMWSWIWLWPWRWMPZWJWIWLFXTWPACVOVPVQVRXRWMXAWPXQWPVSZCBAIVMVNVTWAWB
    WKWLWPXBWNWKWLWPXBWNPXRWQWNWTXAXRWNWQWPYACABIWCVNXQWTWNPZWPWJWLYBWIWJWLFWSW
    NWRWLWJWSWNPCBVOWDWEURVPXRWNXABBIJZXQYCWPWJYCWIWLBVKWFVPCBBIWCVNVTWAWBVJWGW
    H $.
    $( [14-Jul-2009] $)

  ${
    $d x A $.
    $( A closed interval with identical lower and upper bounds is a singleton.
       (Contributed by Jeff Hankins, 13-Jul-2009.) $)
    ccid $p |- ( A e. RR* -> ( A [,] A ) = { A } ) $=
      ( vx cxr wcel cicc co csn cv cle wbr w3a wb elicc1 anidms wceq wi wa clt
      wn xrlenlt ancoms xrlttri3 biimprd exp3a com23 sylbid ex 3impd eleq1a
      xrleid breq2 syl5ibrcom breq1 3jcad impbid elsn syl6bbr bitrd eqrdv ) ACD
      ZBAAEFZAGZUTBHZVADZVCCDZAVCIJZVCAIJZKZVCVBDZUTVDVHLAAVCMNUTVHVCAOZVIUTVHV
      JUTVEVFVGVJUTVEVFVGVJPZPUTVEQZVFVCARJSZVKAVCTVLVGVMVJVLVGAVCRJSZVMVJPVEUT
      VGVNLVCATUAVLVMVNVJVLVMVNVJVEUTVMVNQZVJPVEUTQVJVOVCAUBUCUAUDUEUFUEUFUGUHU
      TVJVEVFVGACVCUIUTVFVJAAIJZAUJZVCAAIUKULUTVGVJVPVQVCAAIUMULUNUOBAUPUQURUS
      $.
      $( [13-Jul-2009] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x C $.  $d x D $.
    $( If the upper bound of one open interval is less than or equal to the
       lower bound of the other, the intervals are disjoint.  (Contributed by
       Jeff Hankins, 13-Jul-2009.) $)
    ioodisj $p |- ( ( ( ( A e. RR* /\ B e. RR* ) /\ ( C e. RR* /\ D e. RR* ) )
     /\ B <_ C ) -> ( ( A (,) B ) i^i ( C (,) D ) ) = (/) ) $=
      ( vx cxr wcel wa cle wbr cv cioo co cin wex wn c0 wceq simpr elin clt wi
      w3a elioo1 3simpb syl6bi ad2antrr wb ad2antlr simp2 xrlttr xrltnle
      3adant2 sylibd exp3a com23 3exp com3r imp5g ad2ant2lr adantr syl2and
      syl5bi 19.23adv mt2d wal eq0 alnex bitr2i sylib ) AFGZBFGZHZCFGZDFGZHZHZB
      CIJZHZEKZABLMZCDLMZNZGZEOZPZWCQRZVSWEVRVQVRSVSWDVRPZEWDVTWAGZVTWBGZHVSWHV
      TWAWBTVSWIVTFGZVTBUAJZHZWJCVTUAJZWHVMWIWMUBVPVRVMWIWKAVTUAJZWLUCWMABVTUDW
      KWOWLUEUFUGVSWJWKWNVTDUAJZUCZWNVPWJWQUHVMVRCDVTUDUIWKWNWPUJUFVQWMWNHWHUBZ
      VRVLVNWRVKVOVLVNWKWLWNWHVNWKVLWLWNWHUBUBZVNWKVLWSVNWKVLUCZWNWLWHWTWNWLWHW
      TWNWLHCBUAJZWHCVTBUKVNVLXAWHUHWKCBULUMUNUOUPUQURUSUTVAVBVCVDVEWGWDPEVFWFE
      WCVGWDEVHVIVJ $.
      $( [13-Jul-2009] $)
  $}

  ${
    $d k m n t u v w x y z $.
    $( Alternate definition of the natural numbers that captures the intuitive
       idea of "building up" from 1.  (Contributed by Jeff Hankins,
       12-Sep-2013.) $)
    dfnn3 $p |- NN = ( rec ( { <. x , y >. | y = ( x + 1 ) } , 1 ) " om ) $=
      ( vz vw vt vv vu vk vn vm cn c1 cv wcel caddc co wral wa cab cint wceq
      copab crdg com cima df-n wss con0 wfn wfun cvv rdgfnon fnfun omex
      funimaex mp2b eleq2 wi wal imbi12d albidv df-ral 3bitr4g anbi12d elab
      wbr wrex c0 peano1 cfv cr 1re elisseti rdg0 wb 0elon fnbrfvbg mp3an mpbi
      breq1 rcla4ev mp2an elima mpbir csuc peano2 adantr nnon rdgsuc syl cop
      funopabeq visset funbrfv opreq1 eqcomd adantl fvex oprex eqeq2d eqeq1
      opelopab sylibr sylan9bb cbvopabv syl6eleq funopfv mpsyl eqtrd suceloni
      mp3an13 3syl mpbid syl11anc r19.23aiva 3imtr4i rgen mpbir2an intss1
      ax-mp ssint imbi1d breq2 eleq1 cbvalv syl6bb imbi2d eqeq1i biimpi
      biimpcd 19.21aiv w3a eqid eleqtri mp2 syl6eq 3ad2ant1 mpbii cla4v
      3ad2ant3 mpd eleq1d rcla4v com12 3ad2ant2 eqeltrd syl5ibcom syl5 3exp
      adantld a2d finds impcom 19.21bi r19.23adva syl5bi ssrdv sylbi mprgbir
      eqssi eqtri ) KLCMZNZDMZLOPZUVBNZDUVBQZRZCSZTZBMZAMZLOPZUAZABUBZLUCZUDUEZ
      CDUFUVJUVQUVQUVINZUVJUVQUGUVRLUVQNZUVEUVQNZDUVQQZUVHUVSUWARCUVQUVPUHUIZUV
      PUJZUVQUKNLUVOULZUHUVPUMZUVPUDUNUOUPUVBUVQUAZUVCUVSUVGUWAUVBUVQLUQUWFUVDU
      VBNZUVFURZDUSZUVDUVQNZUVTURZDUSUVGUWAUWFUWHUWKDUWFUWGUWJUVFUVTUVBUVQUVDUQ
      UVBUVQUVEUQUTVAUVFDUVBVBZUVTDUVQVBVCVDVEUVSUVBLUVPVFZCUDVGZVHUDNVHLUVPVFZ
      UWNVIVHUVPVJZLUAZUWOLUVOLVKVLVMZVNZUWBVHUHNLVKNUWQUWOVOUWDVPVLUHVHLVKUVPV
      QVRVSUWMUWOCVHUDUVBVHLUVPVTWAWBCLUVPUDUWRWCWDUVTDUVQUVBUVDUVPVFZCUDVGEMZU
      VEUVPVFZEUDVGZUWJUVTUWTUXCCUDUVBUDNZUWTRZUVBWEZUDNZUXFUVEUVPVFZUXCUXDUXGU
      WTUVBWFWGUXEUXFUVPVJZUVEUAZUXHUXEUXIUVBUVPVJZUVOVJZUVEUXDUXIUXLUAZUWTUXDU
      VBUHNZUXMUVBWHZLUVBUVOWIWJWGUVOUJZUXEUXKUVEWKZUVONUXLUVEUAABUVMWLZUXEUXQF
      MZGMZLOPZUAZGFUBZUVOUXEUVEUXKLOPZUAZUXQUYCNUWTUYEUXDUWTUXKUVDUAZUYEUWBUWC
      UWTUYFURUWDUWEUVBUVDUVPDWMZWNUPUYFUYDUVEUXKUVDLOWOWPWJWQUYBUXSUYDUAUYEGFU
      XKUVEUVBUVPWRUVDLOWSZUXTUXKUAUYAUYDUXSUXTUXKLOWOWTUXSUVEUYDXAXBXCUYBUVNGF
      ABUXTUVLUAZUYBUXSUVMUAUXSUVKUAUVNUYIUYAUVMUXSUXTUVLLOWOWTUXSUVKUVMXAXDXEX
      FUXKUVEUVOUYHXGXHXIUXDUXJUXHVOZUWTUXDUXNUXFUHNZUYJUXOUVBXJUWBUYKUVEUKNUYJ
      UWDUYHUHUXFUVEUKUVPVQXKXLWGXMUXBUXHEUXFUDUXAUXFUVEUVPVTWAXNXOCUVDUVPUDUYG
      WCEUVEUVPUDUYHWCXPXQXRUVQUVIXSXTUVQUVJUGUVQUXAUGZEUVIEUVQUVIYAUXAUVINLUXA
      NZUVEUXANZDUXAQZRZUYLUVHUYPCUXAEWMUVBUXAUAZUVCUYMUVGUYOUVBUXALUQUYQUWIUVD
      UXANZUYNURZDUSUVGUYOUYQUWHUYSDUYQUWGUYRUVFUYNUVBUXAUVDUQUVBUXAUVEUQUTVAUW
      LUYNDUXAVBVCVDVEUYPCUVQUXAUVBUVQNHMZUVBUVPVFZHUDVGUYPUVBUXANZHUVBUVPUDCWM
      WCUYPVUAVUBHUDUYPUYTUDNZRVUAVUBURZCVUCUYPVUDCUSZUYPUXTUVBUVPVFZVUBURZCUSZ
      URUYPVHIMZUVPVFZVUIUXANZURZIUSZURUYPUXSUVBUVPVFZVUBURZCUSZURUYPUXSWEZVUIU
      VPVFZVUKURZIUSZURUYPVUEURGFUYTUXTVHUAZVUHVUMUYPVVAVUHVHUVBUVPVFZVUBURZCUS
      VUMVVAVUGVVCCVVAVUFVVBVUBUXTVHUVBUVPVTYBVAVVCVULCIUVBVUIUAZVVBVUJVUBVUKUV
      BVUIVHUVPYCUVBVUIUXAYDZUTYEYFYGUXTUXSUAZVUHVUPUYPVVFVUGVUOCVVFVUFVUNVUBUX
      TUXSUVBUVPVTYBVAYGUXTVUQUAZVUHVUTUYPVVGVUHVUQUVBUVPVFZVUBURZCUSVUTVVGVUGV
      VICVVGVUFVVHVUBUXTVUQUVBUVPVTYBVAVVIVUSCIVVDVVHVURVUBVUKUVBVUIVUQUVPYCVVE
      UTYEYFYGUXTUYTUAZVUHVUEUYPVVJVUGVUDCVVJVUFVUAVUBUXTUYTUVBUVPVTYBVAYGUYMVU
      MUYOUYMVULIVUJUYMVUKVUJUWPVUIUAZLVUIUAZUYMVUKVOUWBUWCVUJVVKURUWDUWEVHVUIU
      VPIWMZWNUPVVKVVLUWPLVUIUWSYHYILVUIUXAYDXLYJYKWGUXSUDNZUYPVUPVUTVVNUYOVUPV
      UTURUYMVVNUYOVUPVUTVVNUYOVUPYLZVUSIVURVUQUVPVJZVUIUAZVVOVUKUWBUWCVURVVQUR
      UWDUWEVUQVUIUVPVVMWNUPVVOVVPUXANVVQVUKVVOVVPUXSUVPVJZLOPZUXAVVNUYOVVPVVSU
      AZVUPVVNUXSUHNZVVTUXSWHZVWAVVPVVRUVOVJZVVSLUXSUVOWIUXPVVRVVSWKZUVONVWCVVS
      UAUXRVWDVUIJMZLOPZUAZJIUBZUVOVWDVWHNVVSVVSUAZVVSYMVWGVUIVVSUAVWIJIVVRVVSU
      XSUVPWRZVVRLOWSZVWEVVRUAVWFVVSVUIVWEVVRLOWOWTVUIVVSVVSXAXBWDVWGUVNJIABVWE
      UVLUAZVWGVUIUVMUAVUIUVKUAUVNVWLVWFUVMVUIVWEUVLLOWOWTVUIUVKUVMXAXDXEYNVVRV
      VSUVOVWKXGYOYPWJYQVVOVVRUXANZVVSUXANZVVOUXSVVRUVPVFZVWMVVNUYOVWOVUPVVNVVR
      VVRUAZVWOVVRYMVVNVWAVWPVWOVOZVWBUWBVWAVVRUKNVWQUWDVWJUHUXSVVRUKUVPVQXKWJY
      RYQVUPVVNVWOVWMURZUYOVUOVWRCVVRVWJUVBVVRUAVUNVWOVUBVWMUVBVVRUXSUVPYCUVBVV
      RUXAYDUTYSYTUUAUYOVVNVWMVWNURVUPVWMUYOVWNUYNVWNDVVRUXAUVDVVRUAUVEVVSUXAUV
      DVVRLOWOUUBUUCUUDUUEUUAUUFVVPVUIUXAYDUUGUUHYKUUIUUJUUKUULUUMUUNUUOUUPUUQU
      URUUSUUTUVA $.
      $( [12-Sep-2013] $)
  $}

  $( Euler's constant ` _e ` is greater than 0.  (Contributed by Jeff Hankins,
     22-Nov-2008.) $)
  epos $p |- 0 < _e $=
    ( cc0 c2 clt wbr ceu 2pos c3 egt2lt3 simpli 0re 2re ere lttri mp2an ) ABCDB
    ECDZAECDFOEGCDHIABEJKLMN $.
    $( [22-Nov-2008] $)

  $( Euler's constant ` _e ` is a positive real.  (Contributed by Jeff Hankins,
     22-Nov-2008.) $)
  epr $p |- _e e. RR+ $=
    ( ceu ere epos elrpii ) ABCD $.
    $( [22-Nov-2008] $)

  ${
    $d a k m n y ph $.  $d a k m n x ps $.  $d x y $.
    finminlem.1 $e |- ( x = y -> ( ph <-> ps ) ) $.
    $( A useful lemma about finite sets.  If a property holds for a finite set,
       it holds for a minimal set.  (Contributed by Jeff Hankins,
       4-Dec-2009.) $)
    finminlem $p |- ( E. x e. Fin ph -> E. x ( ph /\ A. y ( ( y C_ x /\ ps ) ->
    x = y ) ) ) $=
      ( vn vm va vk cfn wrex cv cen wbr wa wex com crab c0 wne cin wceq wss wi
      wal hbe1 wcel ax-17 hbrab hbne isfi w3a 19.8a anim2i 3impb breq2 anbi1d
      exbidv elrab sylibr ne0i syl 3exp r19.23aiv sylbi r19.23ai con0 cep wwe
      epweon ssrab2 omsson sstri wefrc mp3an12 hbin hbeq hban simprr wpss wo
      sspss wn ra4e pssss ssfi sylan2 ex sylbir adantrr simprll simprlr
      simplrr visset breq1 anbi12d cla4ev syl11anc csdm adantr php3 cdom cvv
      ssdomg ax-mp endomtr ad2antrr ad2antlr ensym domentr expcom ad2antll
      syld syl5 domnsym con2i nsyli impr word wb nnord ad2antrl ordtri1
      con2bid mpbird jca31 elin anbi1i bitri exp44 r19.23adv syl5bi com23 mpdd
      necon2bd imp31 pm2.21d eqcom biimpi a1i jaod expr imp3a 19.21aiv jca
      eximd imp 3syl ) ACJKCLZFLZMNZAOZCPZFQRZSTZUUNGLZUAZSUBZGUUNKZADLZUUIUCZB
      OUUIUUTUBZUDZDUEZOZCPZAUUOCJCHUUNSUUMCFHQUULCUFHLZQUGCUHUIZUVGSUGCUHZUJUU
      IJUGZUUIUUPMNZGQKZAUUOUDZGUUIUKZUVKUVMGQUUPQUGZUVKAUUOUVOUVKAULZUUPUUNUGZ
      UUOUVPUVOUVKAOZCPZOZUVQUVOUVKAUVTUVRUVSUVOUVRCUMUNUOUUMUVSFUUPQUUJUUPUBZU
      ULUVRCUWAUUKUVKAUUJUUPUUIMUPUQURUSZUTUUNUUPVAVBVCVDVEVFVGVHVIUUNVGUCUUOUU
      SVJUUNQVGUUMFQVKVLVMGVGUUNVNVOUURUVFGUUNUVQUVTUURUVFUDZUWBUVOUVSUWCUVOUUR
      UVSUVFUVOUURUVSUVFUDUVOUUROZUVRUVECUVOUURCUVOCUHCHHUUQSCHUUNUUPUVHUVGUUPU
      GCUHVPUVIVQVRUWDUVRUVEUWDUVROZAUVDUWDUVKAVSUWEUVCDUWEUVABUVBUWEBUVAUVBUWD
      UVRBUVAUVBUDUVAUUTUUIVTZUUTUUIUBZWAUWDUVRBOZOZUVBUUTUUIWBUWIUWFUVBUWGUWIU
      WFUVBUVOUURUWHUWFWCZUVOUWHUURUWJUVOUWHUURUWJUDUVOUWHOZUWFUUQSUWKUWFUUTJUG
      ZUUQSTZUVOUVRUWFUWLUDZBUVOUVKUWNAUVOUVKOZUVLUWNUVKGQWDZUVLUVJUWNUVNUVJUWF
      UWLUWFUVJUVAUWLUUTUUIWEUUIUUTWFWGWHWIVBWJWJUWKUWLUWFUWMUWLUUTILZMNZIQKUWK
      UWFUWMUDZIUUTUKUWKUWRUWSIQUWKUWQQUGZUWRUWFUWMUWKUWTUWROZUWFOZOZUWQUUQUGZU
      WMUXCUWTUUIUWQMNZAOZCPZOZUWQUUPUGZOZUXDUXCUWTUXGUXIUWKUWTUWRUWFWKUXCUWRBU
      XGUWKUWTUWRUWFWLUVOUVRBUXBWMUXFUWRBOCUUTDWNUVBUXEUWRABUUIUUTUWQMWOEWPWQWR
      UXCUXIUUPUWQUCZWCZUWKUXAUWFUXLUWKUXAOZUWFUUTUUIWSNZUXLUXMUVJUWFUXNUDUWKUV
      JUXAUVOUVRUVJBUVOUVKUVJAUWOUVLUVJUWPUVNUTWJWJWTUVJUWFUXNUUIUUTXAWHVBUXMUX
      KUUIUUTXBNZUXNUXKUUPUWQXBNZUXMUXOUUPXCUGUXKUXPUDGWNUUPUWQXCXDXEUXMUXPUUIU
      WQXBNZUXOUWHUXPUXQUDZUVOUXAUVKUXRABUVKUXPUXQUUIUUPUWQXFWHXGXHUWRUXQUXOUDU
      WKUWTUXQUWRUXOUWRUXQUWQUUTMNUXOUUTUWQIWNXIUUIUWQUUTXJWGXKXLXMXNUXOUXNUUIU
      UTXOXPXQXMXRUXCUUPXSZUWQXSZUXIUXLXTUVOUXSUWHUXBUUPYAXGUXAUXTUWKUWFUWTUXTU
      WRUWQYAWTYBUXSUXTOUXKUXIUUPUWQYCYDWRYEYFUXDUWQUUNUGZUXIOUXJUWQUUNUUPYGUYA
      UXHUXIUUMUXGFUWQQUUJUWQUBZUULUXFCUYBUUKUXEAUUJUWQUUIMUPUQURUSYHYIUTUUQUWQ
      VAVBYJYKYLYMYNYOWHYMYPYQUWGUVBUDUWIUWGUVBUUTUUIYRYSYTUUAYLUUBYMUUCUUDUUEW
      HUUFWHYMUUGVEVDUUH $.
      $( [4-Dec-2009] $)
  $}

  ${
    $d x y z u v A $.  $d x y z u v B $.  $d x y z u v C $.  $d x y z u v D $.
    $d x y z u v K $.
    $( A finite intersection of elements taken from a union of collections.
       (Contributed by Jeff Hankins, 15-Nov-2009.) $)
    elfiun $p |- ( ( B e. D /\ C e. K ) -> ( A e. ( fi ` ( B u. C ) ) <-> ( A
    e. ( fi ` B ) \/ A e. ( fi ` C ) \/ E. x e. ( fi ` B ) E. y e. ( fi ` C ) A
    = ( x i^i y ) ) ) ) $=
      ( vz vu vv wcel wa cun cfi cfv cv cin wceq wrex w3o cvv w3a elisset
      adantl simpll simplr 3jca wi 3anim1i 3expib visset inex1 eleq1 mpbiri
      a1i r19.23aivv 3jaoi impcom wss cfn cint wex wb unexg sppfi sylan2 3impb
      df-ss indi eqeq1i bitri c0 uneq12 unidm syl6eq eqeq1d vprc int0 eleq1i
      mtbir biimpcd mtoi pm2.21d inteq eqeq2d imbi1d syl5ibcom 3ad2ant1 com12
      a1i14 sylbid com23 wn uneq2 uncom un0 3eqtr3g adantr syl6bbr simprr
      eqidd sseq1 3anbi123d cla4ev syl111anc wne df-ne inss1 ssn0 mpan sylbir
      ad2antrr intex sylib simprl3 syl11anc mpbird syl5ibrcom 3mix2 syl6 exp43
      simprl2 3mix1 inss2 ssfi sylancl biimpri simp2 mpan2 ad2antlr simp3
      ad2antrl biimpar intun ineq1 ineq2 rcla42ev ex 3mix3 4cases syl5bi 3impd
      19.23adv ssun1 fiss 3adant1 sseld simp1 sylibd ssun2 3ad2ant2 3ad2ant3
      anbi12d eeanv simpl1 simpr1 unss12 unfi 3ad2antr2 3ad2antl2 ineq12
      syl6eqr 3ad2antr3 3ad2antl3 biimpa unex exp31 19.23advv syl5bir
      r19.23advv 3jaod impbid bitrd pm5.21nd ) DFKZEGKZLZCDEMZNOZKZCDNOZKZCENOZ
      KZCAPZBPZQZRZBUWCSAUWASZTZCUAKZUVOUVPUBZUVQUVTLUWKUVOUVPUVTUWKUVQCUVSUCUD
      UVOUVPUVTUEUVOUVPUVTUFUGUWJUVQUWLUWBUVQUWLUHUWDUWIUWBUVOUVPUWLUWBUWKUVOUV
      PCUWAUCUIUJUWDUVOUVPUWLUWDUWKUVOUVPCUWCUCUIUJUWIUVOUVPUWLUWIUWKUVOUVPUWHU
      WKABUWAUWCUWHUWKUHUWEUWAKZUWFUWCKZLZUWHUWKUWGUAKUWEUWFAUKZULCUWGUAUMUNUOU
      PUIUJUQURUWLUVTHPZUVRUSZUWQUTKZCUWQVAZRZUBZHVBZUWJUWKUVOUVPUVTUXCVCZUVQUW
      KUVRUAKZUXDDEFGVDZHCUAUVRUAVEZVFVGZUWLUXCUWJUWLUXBUWJHUWLUWRUWSUXAUWJUWRU
      WQDQZUWQEQZMZUWQRZUWLUWSUXAUWJUHZUHZUWRUWQUVRQZUWQRUXLUWQUVRVHUXOUXKUWQUW
      QDEVIVJVKUXIVLRZUXJVLRZUWLUXLUXNUHUHUXPUXQLZUXLUWLUXNUXRUXLVLUWQRZUWLUXNU
      HZUXRUXKVLUWQUXRUXKVLVLMVLUXIVLUXJVLVMVLVNVOVPUXRUXSUWLUWSUXMUWLUXSUXMUWK
      UVOUXSUXMUHUVPUWKCVLVAZRZUWJUHUXSUXMUWKUYBUWJUWKUYBUYAUAKZUYCUAUAKVQUYAUA
      UAVRVSVTUYBUWKUYCCUYAUAUMWAWBWCUXSUYBUXAUWJUXSUYAUWTCVLUWQWDWEWFWGWHWIWJW
      KWLUXPUXQWMZLZUXLUWLUXNUYEUXLUWQEUSZUXTUYEUXLUXJUWQRUYFUYEUXKUXJUWQUXPUXK
      UXJRUYDUXPUXJUXIMUXJVLMUXKUXJUXIVLUXJWNUXJUXIWOUXJWPWQWRVPUWQEVHWSUYDUYFU
      XTUHUXPUYDUYFUWLUWSUXMUYDUYFLZUWLUWSLZLZUXAUWDUWJUYIUWDUXAUWTUWCKZUYIUYJU
      WEEUSZUWEUTKZUWTUWEVAZRZUBZAVBZUYIUYFUWSUWTUWTRZUYPUYDUYFUYHUFUYGUWLUWSWT
      UYIUWTXAUYOUYFUWSUYQUBAUWQHUKZUWEUWQRZUYKUYFUYLUWSUYNUYQUWEUWQEXBUWEUWQUT
      UMZUYSUYMUWTUWTUWEUWQWDWEZXCXDXEUYIUWTUAKZUVPUYJUYPVCUYIUWQVLXFZVUBUYDVUC
      UYFUYHUYDUXJVLXFZVUCUXJVLXGZUXJUWQUSZVUDVUCUWQEXHZUXJUWQXIXJXKXLUWQXMZXNU
      WKUVOUVPUWSUYGXOAUWTUAEGVEXPXQCUWTUWCUMXRUWDUWBUWIXSXTYAUDWKWLUXPWMZUXQLZ
      UXLUWLUXNVUJUXLUWQDUSZUXTVUJUXLUXIUWQRVUKVUJUXKUXIUWQUXQUXKUXIRVUIUXQUXKU
      XIVLMUXIUXJVLUXIWNUXIWPVOUDVPUWQDVHWSVUIVUKUXTUHUXQVUIVUKUWLUWSUXMVUIVUKL
      ZUYHLZUXAUWBUWJVUMUWBUXAUWTUWAKZVUMVUNUWEDUSZUYLUYNUBZAVBZVUMVUKUWSUYQVUQ
      VUIVUKUYHUFVULUWLUWSWTVUMUWTXAVUPVUKUWSUYQUBAUWQUYRUYSVUOVUKUYLUWSUYNUYQU
      WEUWQDXBUYTVUAXCXDXEVUMVUBUVOVUNVUQVCVUMVUCVUBVUIVUCVUKUYHVUIUXIVLXFZVUCU
      XIVLXGZUXIUWQUSZVURVUCUWQDXHZUXIUWQXIXJXKXLVUHXNUWKUVOUVPUWSVULYBAUWTUADF
      VEXPXQCUWTUWAUMXRUWBUWDUWIYCXTYAWRWKWLVUIUYDLZUWLUXLUWSUXMVVBUWLLZUXLUWSL
      ZLZUXAUWIUWJVVEUXAUWIVVEUXALZUXIVAZUWAKZUXJVAZUWCKZCVVGVVIQZRZUWIVVFVVHVU
      OUYLVVGUYMRZUBZAVBZVVFUXIDUSZUXIUTKZVVGVVGRZVVOVVPVVFUWQDYDUOVVEVVQUXAVVE
      UWSVUTVVQVVCUXLUWSWTVVAUWQUXIYEYFWRVVFVVGXAVVNVVPVVQVVRUBAUXIUWQDUYRULUWE
      UXIRZVUOVVPUYLVVQVVMVVRUWEUXIDXBUWEUXIUTUMVVSUYMVVGVVGUWEUXIWDWEXCXDXEVVF
      VVGUAKZUVOVVHVVOVCVVFVURVVTVVCVURVVDUXAVUIVURUYDUWLVURVUIVUSYGXLXLUXIXMXN
      VVCUVOVVDUXAUWLUVOVVBUWKUVOUVPYHUDXLAVVGUADFVEXPXQVVFVVJUYKUYLVVIUYMRZUBZ
      AVBZVVFUXJEUSZUXJUTKZVVIVVIRZVWCVWDVVFUWQEYDUOVVDVWEVVCUXAUWSVWEUXLUWSVUF
      VWEVUGUWQUXJYEYIUDYJVVFVVIXAVWBVWDVWEVWFUBAUXJUWQEUYRULUWEUXJRZUYKVWDUYLV
      WEVWAVWFUWEUXJEXBUWEUXJUTUMVWGUYMVVIVVIUWEUXJWDWEXCXDXEVVFVVIUAKZUVPVVJVW
      CVCVVFVUDVWHVVCVUDVVDUXAUYDVUDVUIUWLVUDUYDVUEYGYJXLUXJXMXNVVCUVPVVDUXAUWL
      UVPVVBUWKUVOUVPYKUDXLAVVIUAEGVEXPXQVVFCUXKVAZVVKVVECVWIRZUXAUXLVWJUXAVCVV
      CUWSUXLVWIUWTCUXKUWQWDWEYLYMUXIUXJYNVOUWHVVLCVVGUWFQZRABVVGVVIUWAUWCUWEVV
      GRUWGVWKCUWEVVGUWFYOWEUWFVVIRVWKVVKCUWFVVIVVGYPWEYQXEYRUWIUWBUWDYSXTYAYTU
      UAUUBUUCUWLUWBUXCUWDUWIUWLUWBUVTUXCUWLUWAUVSCUVOUVPUWAUVSUSZUWKUVQUXEDUVR
      USVWLUXFDEUUDDUVRUAUUEYFUUFUUGUWLUWKUXEUXDUWKUVOUVPUUHUVOUVPUXEUWKUXFUUFU
      XGXPUUIUWLUWDUVTUXCUWLUWCUVSCUVOUVPUWCUVSUSZUWKUVQUXEEUVRUSVWMUXFEDUUJEUV
      RUAUUEYFUUFUUGUXHUUIUWLUWHUXCABUWAUWCUWLUWOIPZDUSZVWNUTKZUWEVWNVAZRZUBZIV
      BZJPZEUSZVXAUTKZUWFVXAVAZRZUBZJVBZLZUWHUXCUHZUWLUWMVWTUWNVXGUVOUWKUWMVWTV
      CZUVPUWEUAKUVOVXJUWPIUWEUADFVEXJUUKUVPUWKUWNVXGVCZUVOUWFUAKUVPVXKBUKJUWFU
      AEGVEXJUULUUMVXHVWSVXFLZJVBIVBUWLVXIVWSVXFIJUUNUWLVXLVXIIJUWLVXLUWHUXCUWL
      VXLLZUWHLZVWNVXAMZUVRUSZVXOUTKZCVXOVAZRZUXCVXNVWOVXBVXPVXLVWOUWLUWHVWOVWP
      VWRVXFUUOYJVXLVXBUWLUWHVWSVXBVXCVXEUUPYJVWNDVXAEUUQXPVXLVXQUWLUWHVWPVWOVX
      FVXQVWRVWPVXBVXCVXQVXEVWNVXAUURUUSUUTYJVXMUWHVXSVXMUWGVXRCVXLUWGVXRRZUWLV
      WRVWOVXFVXTVWPVWRVXBVXEVXTVXCVWRVXELUWGVWQVXDQVXRUWEVWQUWFVXDUVAVWNVXAYNU
      VBUVCUVDUDWEUVEUXBVXPVXQVXSUBHVXOVWNVXAIUKJUKUVFUWQVXORZUWRVXPUWSVXQUXAVX
      SUWQVXOUVRXBUWQVXOUTUMVYAUWTVXRCUWQVXOWDWEXCXDXEUVGUVHUVIWKUVJUVKUVLUVMUV
      N $.
      $( [15-Nov-2009] $)
  $}

  ${
    $d a b c k m n r s t u v w x y z A $.
    $d a b c k m n r s t u v w x y z B $.
    $( Lemma for ~ fictb . $)
    fictblem $p |- ( ( ( A e. B /\ A ~<_ om ) /\ ( m e. om /\ n e. om ) ) -> (
    n C_ m -> ( ( r e. ( rec ( { <. u , v >. | v = { w | E. y e. u E. z e. u w
    = ( y i^i z ) } } , A ) ` m ) /\ s e. ( rec ( { <. u , v >. | v = { w | E.
    y e. u E. z e. u w = ( y i^i z ) } } , A ) ` n ) ) -> E. x e. om ( r i^i s
    ) e. ( rec ( { <. u , v >. | v = { w | E. y e. u E. z e. u w = ( y i^i z )
    } } , A ) ` x ) ) ) ) $=
      ( vk vc vt va vb wcel com cdom wbr wa cv wss cin wceq wrex cab copab
      crdg cfv wi csuc fveq2 sseq2d ssid a1i id inidm eqcomi ineq1 eqeq2d
      ineq2 rcla42ev mpd3an23 visset eqeq1 2rexbidv elab sylibr ssriv con0
      nnon rdgsuc syl ad2antrr rexeq rexeqbi1dv abbidv sylan9bb cbvopabv
      cbvabv rexbidv cbvrexv rexbii bitri abbii eqtri eqeq2i opabbii fveq1i
      fvex abrexex abrexex2 fvopab syl6req syl5sseq sstr expcom findsg adantll
      sseld anim2d ex peano2 adantr ad2antlr eqid mp3an3 ad2antll inex1 eqtr3i
      syl6eleq eleqtrrd eleq2d rcla4ev syl11anc exp32 syldd ) GHRGSTUAUBZIUCZSR
      ZJUCZSRZUBZUBZYCYAUDZLUCZYAEUCZDUCZBUCZCUCZUEZUFZCFUCZUGZBYOUGZDUHZUFZFEU
      IZGUJZUKZRZKUCZYCUUAUKZRZUBZUUCUUDUUBRZUBZYHUUDUEZAUCZUUAUKZRZASUGZYFYGUU
      GUUIULYFYGUBZUUFUUHUUCUUOUUEUUBUUDYEYGUUEUUBUDZXTUUEUULUDUUEUUEUDZUUEMUCZ
      UUAUKZUDZUUEUURUMZUUAUKZUDZUUPAMYAYCUUKYCUFUULUUEUUEUUKYCUUAUNUOUUKUURUFU
      ULUUSUUEUUKUURUUAUNUOUUKUVAUFUULUVBUUEUUKUVAUUAUNUOUUKYAUFUULUUBUUEUUKYAU
      UAUNUOUUQYDUUEUPUQUURSRZYDUBYCUURUDZUBZUUSUVBUDZUUTUVCULUVFNUCZYAYCUEZUFZ
      JUUSUGZIUUSUGZNUHZUUSUVBOUUSUVMOUCZUUSRZUVNUVIUFZJUUSUGIUUSUGZUVNUVMRUVOU
      VOUVNUVNUVNUEZUFZUVQUVOURUVSUVOUVRUVNUVNUSUTUQUVPUVSUVNUVNYCUEZUFIJUVNUVN
      UUSUUSYAUVNUFUVIUVTUVNYAUVNYCVAVBYCUVNUFUVTUVRUVNYCUVNUVNVCVBVDVEUVLUVQNU
      VNOVFUVHUVNUFUVJUVPIJUUSUUSUVHUVNUVIVGVHVIVJVKUVFUVBUUSYTUKZUVMUVDUVBUWAU
      FZYDUVEUVDUURVLRUWBUURVMGUURYTVNVOVPUWAUUSUUDUVJJYHUGZIYHUGZNUHZUFZLKUIZU
      KUVMUUSYTUWGYTUUDYNCYHUGZBYHUGZDUHZUFZLKUIUWGYSUWKFELKYOYHUFZYSYIUWJUFYIU
      UDUFUWKUWLYRUWJYIUWLYQUWIDYPUWHBYOYHYNCYOYHVQVRVSVBYIUUDUWJVGVTWAUWKUWFLK
      UWJUWEUUDUWJUVHYMUFZCYHUGZBYHUGZNUHUWEUWIUWODNYJUVHUFYNUWMBCYHYHYJUVHYMVG
      VHWBUWOUWDNUWOUVHYAYLUEZUFZCYHUGZIYHUGUWDUWNUWRBIYHYKYAUFZUWMUWQCYHUWSYMU
      WPUVHYKYAYLVAVBWCWDUWRUWCIYHUWQUVJCJYHYLYCUFUWPUVIUVHYLYCYAVCVBWDWEWFWGWH
      WIWJWHWKLKUUSUWEUVMUURUUAWLZUVKINUUSUWTJNUUSUVIUWTWMWNYHUUSUFUWDUVLNUWCUV
      KIYHUUSUVJJYHUUSVQVRVSWOWHWPWQUUTUVGUVCUUEUUSUVBWRWSVOWTXAXBXCXDYFYGUUIUU
      NYFYGUUIUBZUBZYAUMZSRZUUJUXCUUAUKZRZUUNYEUXDXTUXAYBUXDYDYAXEXFXGUXBUUJUUB
      YTUKZUXEUXBUUJUVHPUCZQUCZUEZUFZQUUBUGZPUUBUGZNUHZUXGUXBUUJUXJUFZQUUBUGPUU
      BUGZUUJUXNRUUIUXPYFYGUUCUUHUUJUUJUFZUXPUUJXHUXOUXQUUJYHUXIUEZUFPQYHUUDUUB
      UUBUXHYHUFUXJUXRUUJUXHYHUXIVAVBUXIUUDUFUXRUUJUUJUXIUUDYHVCVBVDXIXJUXMUXPN
      UUJYHUUDLVFXKUVHUUJUFUXKUXOPQUUBUUBUVHUUJUXJVGVHVIVJUUBUVNUXKQUURUGZPUURU
      GZNUHZUFZMOUIZUKUXNUXGMOUUBUYAUXNYAUUAWLZUXLPNUUBUYDQNUUBUXJUYDWMWNUURUUB
      UFUXTUXMNUXSUXLPUURUUBUXKQUURUUBVQVRVSWOUUBUYCYTUYCUVNYNCUURUGZBUURUGZDUH
      ZUFZMOUIYTUYBUYHMOUYAUYGUVNUYAUWMCUURUGZBUURUGZNUHUYGUXTUYJNUXTUVHUXHYLUE
      ZUFZCUURUGZPUURUGUYJUXSUYMPUURUXKUYLQCUURUXIYLUFUXJUYKUVHUXIYLUXHVCVBWDWE
      UYMUYIPBUURUXHYKUFZUYLUWMCUURUYNUYKYMUVHUXHYKYLVAVBWCWDWFWGUYJUYFNDUVHYJU
      FUWMYNBCUURUURUVHYJYMVGVHWBWHWIWJUYHYSMOFEUURYOUFZUYHUVNYRUFUVNYIUFYSUYOU
      YGYRUVNUYOUYFYQDUYEYPBUURYOYNCUURYOVQVRVSVBUVNYIYRVGVTWAWHWKXLXMYEUXEUXGU
      FZXTUXAYBUYPYDYBYAVLRUYPYAVMGYAYTVNVOXFXGXNUUMUXFAUXCSUUKUXCUFUULUXEUUJUU
      KUXCUUAUNXOXPXQXRXS $.
      $( [25-Aug-2009] $)

    $( A set is countable iff its collection of finite intersections is
       countable.  (Contributed by Jeff Hankins, 24-Aug-2009.) $)
    fictb $p |- ( A e. B -> ( A ~<_ om <-> ( fi ` A ) ~<_ om ) ) $=
      ( vx vv vw vy vz vu vt vr vs vc vm vn vk va vb wcel com cdom wbr cfi cfv
      wa cv cin wceq wrex cab copab crdg ciun wss wral cint cfn w3a wex fiv
      sseq1 anbi1d abbidv inteqd sseq2 3anbi1d exbidv eqeq12d visset abfii5
      vtoclg eqtr4d adantr cvv fvex syl6eqelr c0 rdg0g peano1 fveq2 ssiun2s
      ax-mp a1i eqsstr3d eleq2d cbvrexv anbi12i reeanv wi wo word nnord
      ordtri2or2 syl2an adantl ancom fictblem incom eleq1i rexbii syl8ib
      syl7bi ancom2s jaod mpd ex r19.23advv syl5bir syl5bi eliun 3imtr4g
      r19.21aivv omex iunex eleq2 raleqbi1dv anbi12d elab sylanbrc intss1 syl
      ssdomg sylc eqbrtrd domrefg csuc breq1d imbi2d simpr con0 nnon rdgsuc
      rexeq rexeqbi1dv eqeq2d eqeq1 sylan9bb cbvopabv 2rexbidv cbvabv ineq1
      rexbidv ineq2 bitri abbii eqtri eqeq2i opabbii fveq1i abrexex abrexex2
      fvopab syl6eq cxp copab2 wfo wfn crn co wb inex1 eqid fnoprab2 oprvelrn
      simplr oprabval2g syl111anc eqeq1d eqcom syl6bb rexbidva syl5bb abbi2dv
      jctil df-fo sylibr xpex fodom cen xpdom1 xpdom2 domtr syl11anc ad2antll
      xpomen domentr sylancl exp32 a2d finds com12 r19.21aiv iunctb sylancr
      abfi2 impbid ) ABRZASTUAZAUBUCZSTUAZUWPUWQUWSUWPUWQUDZUWRCSCUEZDUEZEUEZFU
      EZGUEZUFZUGZGHUEZUHZFUXHUHZEUIZUGZHDUJZAUKZUCZULZTUAUXPSTUAZUWSUWTUWRAIUE
      ZUMZJUEZKUEZUFZUXRRZKUXRUNZJUXRUNZUDZIUIZUOZUXPTUWPUWRUYHUGUWQUWPUWRUXTAU
      MZUXTUPRZUXRUXTUOUGZUQZJURZIUIZUYHJIABUSLUEZUXRUMZUYEUDZIUIZUOZUXTUYOUMZU
      YJUYKUQZJURZIUIZUGUYHUYNUGLABUYOAUGZUYSUYHVUCUYNVUDUYRUYGVUDUYQUYFIVUDUYP
      UXSUYEUYOAUXRUTVAVBVCVUDVUBUYMIVUDVUAUYLJVUDUYTUYIUYJUYKUYOAUXTVDVEVFVBVG
      IJKUYOLVHVIVJVKVLZUWTUYHVMRUYHUXPUMZUYHUXPTUAUWTUYHUWRVMVUEAUBVNVOUWTUXPU
      YGRZVUFUWTAUXPUMZUYBUXPRZKUXPUNZJUXPUNZVUGUWTAVPUXNUCZUXPUWPVULAUGUWQABUX
      MVQVLZVULUXPUMZUWTVPSRVUNVRCSUXOVPVULUXAVPUXNVSVTWAWBWCUWTVUIJKUXPUXPUWTU
      XTUXORZCSUHZUYAUXORZCSUHZUDZUYBUXORZCSUHZUXTUXPRZUYAUXPRZUDVUIVUSUXTMUEZU
      XNUCZRZMSUHZUYANUEZUXNUCZRZNSUHZUDZUWTVVAVUPVVGVURVVKVUOVVFCMSUXAVVDUGUXO
      VVEUXTUXAVVDUXNVSWDWEVUQVVJCNSUXAVVHUGUXOVVIUYAUXAVVHUXNVSWDWEWFVVLVVFVVJ
      UDZNSUHMSUHUWTVVAVVFVVJMNSSWGUWTVVMVVAMNSSUWTVVDSRZVVHSRZUDZVVMVVAWHZUWTV
      VPUDZVVDVVHUMZVVHVVDUMZWIZVVQVVPVWAUWTVVNVVDWJVVHWJVWAVVOVVDWKVVHWKVVDVVH
      WLWMWNVVRVVSVVQVVTUWTVVOVVNVVSVVQWHVVMVVJVVFUDZUWTVVOVVNUDUDZVVSVVAVVFVVJ
      WOVWCVVSVWBUYAUXTUFZUXORZCSUHVVACFGEDHABNMJKWPVWEVUTCSVWDUYBUXOUYAUXTWQWR
      WSWTXAXBCFGEDHABMNKJWPXCXDXEXFXGXHVVBVUPVVCVURCUXTSUXOXICUYASUXOXIWFCUYBS
      UXOXIXJXKUYFVUHVUKUDIUXPCSUXOXLUXAUXNVNZXMUXRUXPUGUXSVUHUYEVUKUXRUXPAVDUY
      DVUJJUXRUXPUYCVUIKUXRUXPUXRUXPUYBXNXOXOXPXQXRUXPUYGXSXTUYHUXPVMYAYBYCUWTS
      STUAZUXOSTUAZCSUNUXQSVMRVWGXLSVMYDWAUWTVWHCSUXASRUWTVWHUWTUXRUXNUCZSTUAZW
      HUWTVULSTUAZWHUWTOUEZUXNUCZSTUAZWHUWTVWLYEZUXNUCZSTUAZWHUWTVWHWHIOUXAUXRV
      PUGZVWJVWKUWTVWRVWIVULSTUXRVPUXNVSYFYGUXRVWLUGZVWJVWNUWTVWSVWIVWMSTUXRVWL
      UXNVSYFYGUXRVWOUGZVWJVWQUWTVWTVWIVWPSTUXRVWOUXNVSYFYGUXRUXAUGZVWJVWHUWTVX
      AVWIUXOSTUXRUXAUXNVSYFYGUWTVULASTVUMUWPUWQYHYCVWLSRZUWTVWNVWQVXBUWTVWNVWQ
      VXBUWTVWNUDZUDZVWPUYOVVDVVHUFZUGZNVWMUHZMVWMUHZLUIZSTVXDVWPVWMUXMUCZVXIVX
      BVWPVXJUGZVXCVXBVWLYIRVXKVWLYJAVWLUXMYKXTVLVXJVWMUYAVXFNUXTUHZMUXTUHZLUIZ
      UGZJKUJZUCVXIVWMUXMVXPUXMUYAUXGGUXTUHZFUXTUHZEUIZUGZJKUJVXPUXLVXTHDJKUXHU
      XTUGZUXLUXBVXSUGUXBUYAUGVXTVYAUXKVXSUXBVYAUXJVXREUXIVXQFUXHUXTUXGGUXHUXTY
      LYMVBYNUXBUYAVXSYOYPYQVXTVXOJKVXSVXNUYAVXSUYOUXFUGZGUXTUHZFUXTUHZLUIVXNVX
      RVYDELUXCUYOUGUXGVYBFGUXTUXTUXCUYOUXFYOYRYSVYDVXMLVYDUYOVVDUXEUFZUGZGUXTU
      HZMUXTUHVXMVYCVYGFMUXTUXDVVDUGZVYBVYFGUXTVYHUXFVYEUYOUXDVVDUXEYTYNUUAWEVY
      GVXLMUXTVYFVXFGNUXTUXEVVHUGVYEVXEUYOUXEVVHVVDUUBYNWEWSUUCUUDUUEUUFUUGUUEU
      UHJKVWMVXNVXIVWLUXNVNZVXGMLVWMVYINLVWMVXEVYIUUIUUJUXTVWMUGVXMVXHLVXLVXGMU
      XTVWMVXFNUXTVWMYLYMVBUUKUUEUULVXDVXIVWMVWMUUMZTUAZVYJSTUAZVXISTUAVXDVYJVX
      IPUEZVWMRQUEZVWMRUDUXRVYMVYNUFZUGUDPQIUUNZUUOZVYKVXDVYPVYJUUPZVYPUUQZVXIU
      GZUDVYQVXDVYTVYRVXDVXHLVYSUYOVYSRZVVDVVHVYPUURZUYOUGZNVWMUHZMVWMUHZVXDVXH
      VYRWUAWUEUUSPQIVWMVWMVYOVYPVYMVYNPVHUUTVYPUVAZUVBZMNVWMVWMUYOVYPUVCWAVXDW
      UDVXGMVWMVXDVVDVWMRZUDZWUCVXFNVWMWUIVVHVWMRZUDZWUCVXEUYOUGVXFWUKWUBVXEUYO
      WUKWUHWUJVXEVMRZWUBVXEUGVXDWUHWUJUVDWUIWUJYHWULWUKVVDVVHMVHUUTWBPQIVVDVVH
      VWMVWMVYOVXEVYPVVDVYNUFVMVYMVVDVYNYTVYNVVHVVDUUBWUFUVEUVFUVGVXEUYOUVHUVIU
      VJUVJUVKUVLWUGUVMVYJVXIVYPUVNUVOVYJVXIVYPVWMVWMVYIVYIUVPUVQXTVXDVYJSSUUMZ
      TUAZWUMSUVRUAVYLVWNWUNVXBUWTVWNVYJSVWMUUMZTUAWUOWUMTUAWUNVWMSVWMXLVYIUVSV
      WMSSXLXLUVTVYJWUOWUMUWAUWBUWCUWDVYJWUMSUWEUWFVXIVYJSUWAUWBYCUWGUWHUWIUWJU
      WKCSUXOXLVWFUWLUWMUWRUXPSUWAUWBXEUWPAUWRTUAZUWSUWQWHUWPAUWRUMWUPABUWNAUWR
      BYAXDWUPUWSUWQAUWRSUWAXEXTUWO $.
      $( [24-Aug-2009] $)
  $}

  ${
    $d b c f j k n t w x y z A $.  $d b c f j k n t w x y z B $.
    $( A finite subset of the union of a superset chain is a subset of some
       element of the chain.  A useful preliminary result for ~ alexsub and
       others.  (Contributed by Jeff Hankins, 25-Jan-2010.) $)
    finsschain $p |- ( ( ( A =/= (/) /\ A. x e. A A. y e. A ( x C_ y \/ y C_ x
    ) ) /\ ( B e. Fin /\ B C_ U. A ) ) -> E. z e. A B C_ z ) $=
      ( vb vk vn vj vc vf vw vt c0 wne cv wss wo wral wa cfn wcel cuni wrex wi
      wceq sseq1 rexbidv imbi12d imbi2d cen wbr com isfi wal csuc breq2 imbi1d
      albidv en0 wex n0 0ss sseq2 rcla4ev mpan2 syl5ibr 19.23adv syl5bi a1dd
      adantrd sylbi ax-gen breq1 cbvalv wf1o visset sucex bren ccnv f1ocnv
      cima cvv cnvex imaexg ax-mp cla4v cres wf1 f1of1 sssucid f1ores syl
      ad2antlr f1oen ensym 3syl simprr crn imassrn wfo f1ofo forn syl5sseq
      simprl sstrd cbvrexv cfv csn wf f1of sucid ffvelrn sseldd eluni2 sylib
      snssi reximi cun orbi12d rcla42v ad2ant2r ad2antrr simplr jca unss
      syl11anc exp58 simpll simplrr simpr simpllr adantr jaod syld com23 imp3a
      com14 imp42 wfn f1ofn adantl a1i fnsnfv uneq2d df-suc imaeq2i imaundi
      eqtr2i syl6eq foima eqtrd sseq1d mpbid exp512 r19.23adv mpd imim2d mpid
      syl5 exp43 com25 19.21adv finds 19.21bi r19.23aiv vtoclga com12 imp32 )
      DNOZAPZBPZQZUVLUVKQZRZBDSADSZTZEUAUBZEDUCZQZECPZQZCDUDZUVRUVQUVTUWCUEZUVQ
      FPZUVSQZUWEUWAQZCDUDZUEZUEZUVQUWDUEFEUAUWEEUFZUWIUWDUVQUWKUWFUVTUWHUWCUWE
      EUVSUGUWKUWGUWBCDUWEEUWAUGUHUIUJUWEUAUBUWEGPZUKULZGUMUDUWJGUWEUNUWMUWJGUM
      UWLUMUBUWMUWJUEZFUWEHPZUKULZUWJUEZFUOUWENUKULZUWJUEZFUOUWEIPZUKULZUWJUEZF
      UOZUWEUWTUPZUKULZUWJUEZFUOZUWNFUOHIUWLUWONUFZUWQUWSFUXHUWPUWRUWJUWONUWEUK
      UQURUSUWOUWTUFZUWQUXBFUXIUWPUXAUWJUWOUWTUWEUKUQURUSUWOUXDUFZUWQUXFFUXJUWP
      UXEUWJUWOUXDUWEUKUQURUSUWOUWLUFZUWQUWNFUXKUWPUWMUWJUWOUWLUWEUKUQURUSUWSFU
      WRUWENUFZUWJUWEUTUXLUVJUWIUVPUXLUVJUWHUWFUVJUVKDUBZAVAUXLUWHADVBUXLUXMUWH
      AUXMUWHUXLNUWAQZCDUDZUXMNUVKQZUXOUVKVCUXNUXPCUVKDUWAUVKNVDVEVFUXLUWGUXNCD
      UWENUWAUGUHVGVHVIVJVKVLVMUXCJPZUWTUKULZUVQUXQUVSQZUXQUWAQZCDUDZUEZUEZUEZJ
      UOZUWTUMUBZUXGUXBUYDFJUWEUXQUFZUXAUXRUWJUYCUWEUXQUWTUKVNUYGUWIUYBUVQUYGUW
      FUXSUWHUYAUWEUXQUVSUGUYGUWGUXTCDUWEUXQUWAUGUHUIUJUIVOUYFUYEUXFFUYFUWFUXEU
      VQUYEUWHUYFUXEUWFUVQUYEUWHUEZUEZUXEUWEUXDKPZVPZKVAUYFUWFUYIUEZUWEUXDKUWTI
      VQZVRVSUYFUYKUYLKUYKUXDUWEUYJVTZVPZUYFUYLUWEUXDUYJWAUYFUYOUWFUVQUYHUYEUYN
      UWTWBZUWTUKULZUVQUYPUVSQZUYPUWAQZCDUDZUEZUEZUEZUYFUYOTZUWFUVQTZTZUWHUYDVU
      CJUYPUYNWCUBUYPWCUBUYJKVQWDUYNUWTWCWEWFZUXQUYPUFZUXRUYQUYCVUBUXQUYPUWTUKV
      NVUHUYBVUAUVQVUHUXSUYRUYAUYTUXQUYPUVSUGVUHUXTUYSCDUXQUYPUWAUGUHUIUJUIWGVU
      FVUCUYQUWHVUFUWTUYPUYNUWTWHZVPZUWTUYPUKULUYQUYOVUJUYFVUEUYOUXDUWEUYNWIZVU
      JUXDUWEUYNWJVUKUWTUXDQVUJUWTWKUXDUWEUWTUYNWLVFWMWNUWTUYPVUIUYMWOUWTUYPVUG
      WPWQVUFVUBUWHUYQVUFVUBUVQUWHVUDUWFUVQWRVUFVUAUWHUVQVUFVUAUYRUWHVUFUYPUWEU
      VSUYOUYPUWEQUYFVUEUYOUYNWSZUYPUWEUYNUWTWTUYOUXDUWEUYNXAZVULUWEUFUXDUWEUYN
      XBZUXDUWEUYNXCWMXDWNVUDUWFUVQXEZXFVUFUYTUWHUYRUYTUYPLPZQZLDUDVUFUWHUYSVUQ
      CLDUWAVUPUYPVDXGVUFVUQUWHLDVUFUWTUYNXHZXIZMPZQZMDUDZVUPDUBZVUQUWHUEUEZVUF
      VURVUTUBZMDUDZVVBVUFVURUVSUBVVFVUFUWEUVSVURVUOUYOVURUWEUBZUYFVUEUYOUXDUWE
      UYNXJZVVGUXDUWEUYNXKVVHUWTUXDUBZVVGUWTUYMXLZUXDUWEUWTUYNXMVFWMWNXNMVURDXO
      XPVVEVVAMDVURVUTXQXRWMVUFVVAVVDMDVUFVUTDUBZVVAVVCVUQUWHVUFVVKVVATZVVCVUQT
      ZTZTZUYPVUSXSZUWAQZCDUDZUWHVUDUWFUVQVVNVVRVVNUWFUVQVUDVVRVVNUVQUWFVUDVVRU
      EZVVNUVJUVPUWFVVSUEZVVNUVPUVJVVTVVNUVPVUTVUPQZVUPVUTQZRZUVJVVTUEZVVKVVCUV
      PVWCUEVVAVUQUVOVWCVUTUVLQZUVLVUTQZRABVUTVUPDDUVKVUTUFUVMVWEUVNVWFUVKVUTUV
      LUGUVKVUTUVLVDXTUVLVUPUFVWEVWAVWFVWBUVLVUPVUTVDUVLVUPVUTUGXTYAYBVVNVWAVWD
      VWBVVNVWAUVJUWFVUDVVRVVNVWATUVJUWFTVUDTZTZVVCVVPVUPQZVVRVVNVVCVWAVWGVVLVV
      CVUQXEYCVWHVUQVUSVUPQZTVWIVWHVUQVWJVVNVUQVWAVWGVVLVVCVUQWRYCVWHVUSVUTVUPV
      VNVVAVWAVWGVVKVVAVVMYDYCVVNVWAVWGYDXFYEUYPVUSVUPYFXPVVQVWICVUPDUWAVUPVVPV
      DVEYGYHVVNVWBUVJUWFVUDVVRVVNVWBTZVWGTZVVKVVPVUTQZVVRVVNVVKVWBVWGVVKVVAVVM
      YIYCVWLUYPVUTQZVVATZVWMVWKVWOVWGVWKVWNVVAVWKUYPVUPVUTVVLVVCVUQVWBYJVVNVWB
      YKXFVVKVVAVVMVWBYLYEYMUYPVUSVUTYFXPVVQVWMCVUTDUWAVUTVVPVDVEYGYHYNYOYPYQYP
      YRYSVVOVVQUWGCDVVOVVPUWEUWAVVOVVPUYNUXDWBZUWEVVOVVPUYPUYNUWTXIZWBZXSZVWPV
      VOVUSVWRUYPVVOUYNUXDYTZVVIVUSVWRUFVUDVWTVUEVVNUYOVWTUYFUXDUWEUYNUUAUUBYCV
      VIVVOVVJUUCUXDUWTUYNUUDYGUUEVWPUYNUWTVWQXSZWBVWSUXDVXAUYNUWTUUFUUGUYNUWTV
      WQUUHUUIUUJVUDVWPUWEUFZVUEVVNUYOVXBUYFUYOVUMVXBVUNUXDUWEUYNUUKWMUUBYCUULU
      UMUHUUNUUOUUPUUQUUPVIUURUUSUURUUSUURUUSUUTUVAUUTVHVIYPUVBUVCVIUVDUVEUVFVL
      UVGUVHUVI $.
      $( [25-Jan-2010] $)
  $}

  ${
    $( An average of two numbers is less than or equal to one of them if and
       only if the other number is.  (Contributed by Jeff Hankins,
       15-Sep-2013.) $)
    avgle2 $p |- ( ( A e. RR /\ B e. RR ) -> ( ( ( A + B ) / 2 ) <_ B <-> A <_
    B ) ) $=
      ( cr wcel wa caddc co c2 cmul cle wbr cdiv wb cc wceq recn 2times syl
      breq2d adantl readdcl cc0 clt 2re 2pos pm3.2i ledivmul mp3an3 sylancom
      leadd1 3anidm23 3bitr4d ) ACDZBCDZEABFGZHBIGZJKZUOBBFGZJKZUOHLGBJKZABJKZU
      NUQUSMUMUNUPURUOJUNBNDUPUROBPBQRSTUMUNUOCDZUTUQMZABUAVBUNHCDZUBHUCKZEVCVD
      VEUDUEUFUOBHUGUHUIUMUNVAUSMABBUJUKUL $.
      $( [15-Sep-2013] $)
  $}

  ${
    $d x y z w A $.  $d x y z w B $.
    $( Any lower bound of a nonempty set of real numbers is less than or equal
       to its infimum.  (Contributed by Jeff Hankins, 1-Sep-2013.) $)
    lbleinf $p |- ( ( ( A C_ RR /\ A =/= (/) /\ B e. RR ) /\ A. x e. A B <_ x )
    -> B <_ sup ( A , RR , `' < ) ) $=
      ( vy vz vw cr wss c0 wne wcel w3a cv cle wbr wral wa clt ccnv csup wn
      wrex wi simpl1 simpl2 wceq breq1 ralbidv rcla4ev breq2 cbvralv rexbii
      sylib 3ad2antl3 infm3 syl111anc wb brcnvg notbid ralbidva adantl ancoms
      adantll cbvrexv rexbidva syl5bb imbi12d anbi12d mpbird simpl3 ssel2
      3ad2antl1 lenlt syl11anc bitr4d biimpd ralimdva imp jca wor ltso cnvso
      mpbi supnub sylc infmrcl ) BGHZBIJZCGKZLZCAMZNOZABPZQZCBGRSZTZNOZCWPWOOZU
      AZWNDMZEMZWOOZUAZEBPZXAWTWOOZXAWKWOOZABUBZUCZEGPZQZDGUBZWICWKWOOZUAZABPZQ
      WSWNXKXAWTROZUAZEBPZWTXAROZFMZXAROZFBUBZUCZEGPZQZDGUBZWNWGWHWTXANOZEBPZDG
      UBZYEWGWHWIWMUDZWGWHWIWMUEZWIWGWMYHWHWIWMQWTWKNOZABPZDGUBZYHYLWMDCGWTCUFY
      KWLABWTCWKNUGUHUIZYLYGDGYKYFAEBWKXAWTNUJUKULUMUNDEFBUOUPWNXJYDDGWNWTGKZQZ
      XDXQXIYCYOXDXQUQWNYOXCXPEBYOXABKQXBXOWTXAGBRURUSUTVAYPXHYBEGYPXAGKZQZXEXR
      XGYAYOYQXEXRUQZWNYQYOYSXAWTGGRURVBVCXGXAXSWOOZFBUBZYRYAXFYTAFBWKXSXAWOUJV
      DYQUUAYAUQYPYQYTXTFBXAXSGBRURVEVAVFVGUTVHVEVIWNWIXNWGWHWIWMVJZWJWMXNWJWLX
      MABWJWKBKZQZWLXMUUDWLWKCROZUAZXMUUDWIWKGKZWLUUFUQWGWHWIUUCVJWGWHUUCUUGWIB
      GWKVKVLCWKVMVNUUDXLUUEWIWGUUCXLUUEUQWHCWKGBRURUNUSVOVPVQVRVSDEAGBCWOGRVTG
      WOVTWAGRWBWCWDWEWNWQWPCROZUAZWSWNWIWPGKZWQUUIUQUUBWNWGWHYMUUJYIYJWIWGWMYM
      WHYNUNDABWFUPZCWPVMVNWNWRUUHWNWIUUJWRUUHUQUUBUUKCWPGGRURVNUSVOVI $.
      $( [1-Sep-2013] $)
  $}

  ${
    $d x y A $.  $d x y z S $.
    $( If a nonempty set of real numbers has a lower bound, its infimum is less
       than or equal to any of its elements.  (Contributed by Jeff Hankins,
       15-Sep-2013.) $)
    infle $p |- ( ( S C_ RR /\ E. x e. RR A. y e. S x <_ y /\ A e. S ) -> sup (
    S , RR , `' < ) <_ A ) $=
      ( vz cr wss cv cle wbr wral wrex wcel w3a clt ccnv csup wn wi wa c0 wne
      simp1 ne0i 3ad2ant3 simp2 infm3 syl111anc visset brcnv notbii ralbii
      rexbii imbi12i anbi12i sylibr simp3 wor ltso cnvso mpbi supub sylc wb
      infmrcl ssel2 3adant2 lenlt syl11anc brcnvg notbid bitr4d mpbird ) DFGZAH
      ZBHZIJBDKAFLZCDMZNZDFOPZQZCIJZWACVTJZRZVSVOVPVTJZRZBDKZVPVOVTJZVPEHZVTJZE
      DLZSZBFKZTZAFLZVRWDVSVPVOOJZRZBDKZVOVPOJZWIVPOJZEDLZSZBFKZTZAFLZWOVSVNDUA
      UBZVQXEVNVQVRUCZVRVNXFVQDCUDUEZVNVQVRUFZABEDUGUHWNXDAFWGWRWMXCWFWQBDWEWPV
      OVPOAUIZBUIZUJUKULWLXBBFWHWSWKXAVPVOOXKXJUJWJWTEDVPWIOXKEUIUJUMUNULUOUMUP
      VNVQVRUQZABEFDCVTFOURFVTURUSFOUTVAVBVCVSWBCWAOJZRZWDVSWAFMZCFMZWBXNVDVSVN
      XFVQXOXGXHXIABDVEUHZVNVRXPVQDFCVFVGWACVHVIVSWCXMVSXOVRWCXMVDXQXLWACFDOVJV
      IVKVLVM $.
      $( [15-Sep-2013] $)
  $}

