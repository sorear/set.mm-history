$(
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
                Ultrafilters
=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
$)

  $c UFil $.
  $( Extend class notation with the class of all ultrafilters. $)
  cufil $a class UFil $.

  ${
    $d f x $.
    $( Define the class of all ultrafilters. $)
    df-ufil $a |- UFil = { f e. Fil | A. x e. ~P U. f ( x e. f \/ ( U. f \ x )
    e. f ) } $.
  $}

  ${
    $d f t w x y z F $.  $d f t w x y z X $.
    isufil.1 $e |- X = U. F $.
    $( The property of being an ultrafilter. $)
    isufil $p |- ( F e. UFil <-> ( F e. Fil /\ A. x e. ~P X ( x e. F \/ ( X \ x
    ) e. F ) ) ) $=
      ( vf cv wcel cuni cdif wo cpw wral cfil cufil wceq unieq syl6eqr pweq
      syl eleq2 difeq1d id eleq12d orbi12d raleqbidv df-ufil elrab2 ) AFZEFZGZU
      IHZUHIZUIGZJZAUKKZLUHBGZCUHIZBGZJZACKZLEBMNUIBOZUNUSAUOUTVAUKCOUOUTOVAUKB
      HCUIBPDQZUKCRSVAUJUPUMURUIBUHTVAULUQUIBVAUKCUHVBUAVAUBUCUDUEAEUFUG $.
      $( [30-Nov-2009] $)

    $( The maximal property of an ultrafilter. $)
    isufil2 $p |- ( F e. UFil <-> ( F e. Fil /\ A. f e. Fil ( ( X = U. f /\ F
    C_ f ) -> F = f ) ) ) $=
      ( vx vt vy vz vw cufil wcel cfil cv cdif wo cpw wral wa cuni wceq wss wi
      isufil simpr a1i elssuni visset elpw sylibr ad2antrl pweq adantr
      ad2antll eleqtrrd eleq1 difeq2 eleq1d orbi12d rcla4v syl wn c0 filesn
      ad2antlr cin simpllr simplrl simprr sseldd filint syl111anc difdisj
      syl5eqelr mtand pm2.27 orcom df-or bitri syl5ib syld exp32 com24 imp32
      ssrdv expr jcad eqss syl6ibr exp31 com23 r19.21adv wne csn cun cfi cfv
      cfg ssequn2 biimpi unisn eqcomi uneq12i uniun eqtr4i syl5reqr cvv snex
      unexg mpan2 fiuni ad2antrr cfbas wnel wb fsubbas filusb ne0i ssun1 ssn0
      mpan 3syl ineq2 neeq1d ineq1 rcla4cv syl5cbir elsn syl7ib imp3a
      r19.21aivv filfbas imp inss2 oefil2 adantlr fbunfip syl11anc mpbird
      mpbir2and eqid fgbas 3eqtrd abfi2 sstrd fbssfg jca fgfil unieq eqeq2d
      sseq2 anbi12d eqeq2 imbi12d mpid eleq2 ssun2 snid ex orc syl8 wrex w3a
      simpll simprl difss syl6ssr reldisj adantl biimpd impr 3jca fillsb sylc
      a1i4 r19.23adv rexnal nne rexbii bitr3i olc pm2.61d impbid pm5.32i ) BJKB
      LKZEMZBKZCUWENZBKZOZECPZQZRUWDCAMZSZTZBUWLUAZRZBUWLTZUBZALQZREBCDUCUWDUWK
      UWSUWDUWKUWSUWDUWKUWRALUWDUWLLKZUWKUWRUWDUWTUWKUWRUWDUWTRZUWKRZUWPUWOUWLB
      UAZRUWQUXBUWPUWOUXCUWPUWOUBUXBUWNUWOUDUEUXAUWKUWPUXCUXAUWKUWPRRFUWLBUXAUW
      KUWPFMZUWLKZUXDBKZUBUXAUXEUWPUWKUXFUXAUXEUWPUWKUXFUBUXAUXEUWPRZRZUWKUXFCU
      XDNZBKZOZUXFUXHUXDUWJKUWKUXKUBUXHUXDUWMPZUWJUXEUXDUXLKZUXAUWPUXEUXDUWMUAU
      XMUXDUWLUFUXDUWMFUGUHUIUJUWPUWJUXLTZUXAUXEUWNUXNUWOCUWMUKULUMUNUWIUXKEUXD
      UWJUWEUXDTZUWFUXFUWHUXJUWEUXDBUOUXOUWGUXIBUWEUXDCUPUQURUSUTUXHUXJVAZUXFUB
      ZUXFUXKUXHUXPUXQUXFUBUXHUXJVBUWLKZUWTUXRVAUWDUXGUWLVCVDUXHUXJRZUXDUXIVEZU
      WLVBUXSUWTUXEUXIUWLKUXTUWLKUWDUWTUXGUXJVFUXAUXEUWPUXJVGUXSBUWLUXIUXGUWOUX
      AUXJUXEUWNUWOVHVDUXHUXJUDVIUXDUXIUWLVJVKUXDCVLVMVNUXPUXFVOUTUXKUXJUXFOUXQ
      UXFUXJVPUXJUXFVQVRVSVTWAWBWCWDWEWFBUWLWGWHWIWJWKUWDUWSUWIEUWJUWDUWSUWECUA
      ZUWIUWEUWJKUWDUYAUWSUWIUWDUYAUWSUWIUBZUWDUYARZGMZUWEVEZVBWLZGBQZUYBUYCUYG
      UWSUWFUWIUYCUYGUWSUWFUBUYCUYGRZUWSBBUWEWMZWNZWOWPZWQWPZTZUWFUYHUWSCUYLSZT
      ZBUYLUAZRZUYMUYHUYOUYPUYHCUYJSZUYKSZUYNUYHCUWEWNZCUYRUYAUYTCTZUWDUYGUYAVU
      AUWECWRWSVDUYTBSZUYISZWNUYRCVUBUWEVUCDVUCUWEUWEEUGZWTXAXBBUYIXCXDXEUWDUYR
      UYSTZUYAUYGUWDUYJXFKZVUEUWDUYIXFKVUFUWEXGBUYILXFXHXIZUYJXFXJUTXKUYHUYKXLK
      ZUYSUYNTUYHVUHUYJVBWLZVBUYKXMZUWDVUHVUIVUJRXNZUYAUYGUWDVUFVUKVUGUYJXFXOUT
      XKUWDVUIUYAUYGUWDCBKZBVBWLZVUIBCDXPZBCXQBUYJUAZVUMVUIBUYIXRZBUYJXSXTYAXKU
      YHVUJHMZIMZVEZVBWLZIUYIQHBQZUYHVUTHIBUYIUYHVUQBKZVURUYIKZVUTUYHVVBVURUWET
      ZVUTVVCUYCUYGVVBVVDVUTUBVVDVUTVUQUWEVEZVBWLZUYCUYGVVBRRVVDVUSVVEVBVURUWEV
      UQYBYCUYCUYGVVBVVFUYGVVBVVFUBUBUYCUYFVVFGVUQBUYDVUQTUYEVVEVBUYDVUQUWEYDYC
      YEUEWCYFWEIUWEYGYHYIYJUYHBXLKZUYIXLKZVUJVVAXNUWDVVGUYAUYGBYKXKUYHUYILKZVV
      HUWDUYGVVIUYAUWDUYGRCUWEVEZVBWLZUWEVBWLZVVIUWDUYGVVKUWDVULUYGVVKUBVUNUYFV
      VKGCBUYDCTUYEVVJVBUYDCUWEYDYCUSUTYLVVJUWEUAVVKVVLCUWEYMVVJUWEXSXTUWEXFKVV
      LVVIVUDUWEXFYNXTYAYOUYIYKUTHIBUYIYPYQYRYSZUYKUYSUYSYTUUAUTUUBUYHBUYKUYLUY
      HBUYJUYKVUOUYHVUPUEUWDUYJUYKUAZUYAUYGUWDVUFVVNVUGUYJXFUUCUTXKZUUDUYHVUHUY
      KUYLUAVVMUYKUUEUTZUUDUUFUYHVUHUYLLKUWSUYQUYMUBZUBVVMUYKUUGUWRVVQAUYLLUWLU
      YLTZUWPUYQUWQUYMVVRUWNUYOUWOUYPVVRUWMUYNCUWLUYLUUHUUIUWLUYLBUUJUUKUWLUYLB
      UULUUMUSYAUUNUYMUWFUWEUYLKUYHBUYLUWEUUOUYHUYIUYLUWEUYHUYIUYKUYLUYHUYIUYJU
      YKUYIUYJUAUYHUYIBUUPUEVVOUUDVVPUUDUWEUYIKUYHUWEVUDUUQUEVIYFVTUURUWFUWHUUS
      UUTUYCUYGVAZUWSUWHUWIUYCUYEVBTZGBUVAZUWSUWHUBZVVSUYCVVTVWBGBUYCUYDBKZVVTU
      WSUWHUYCVWCVVTUWHUYCVWCVVTRZRZUWDVWCUWGCUAZUYDUWGUAZUVBUWHUWDUYAVWDUVCVWE
      VWCVWFVWGUYCVWCVVTUVDVWFVWECUWEUVEUEUYCVWCVVTVWGUYCVWCRVVTVWGVWCVVTVWGXNZ
      UYCVWCUYDCUAVWHVWCUYDVUBCUYDBUFDUVFUYDUWECUVGUTUVHUVIUVJUVKUYDUWGBCDUVLUV
      MWAUVNUVOVVSUYFVAZGBUVAVWAUYFGBUVPVWIVVTGBUYEVBUVQUVRUVSVSUWHUWFUVTUUTUWA
      UURWJUWECVUDUHYHWKUWBUWCVR $.
      $( [30-Nov-2009] $)
  $}

  ${
    $d x F $.
    $( An ultrafilter is a filter. $)
    ufilfil $p |- ( F e. UFil -> F e. Fil ) $=
      ( vx cufil wcel cfil cv cuni cdif wo cpw wral eqid isufil simplbi ) ACDAE
      DBFZADAGZOHADIBPJKBAPPLMN $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d s F $.  $d s S $.  $d s X $.
    ufilss.1 $e |- X = U. F $.
    $( For any subset of the base set of an ultrafilter, either the set is in
       the ultrafilter or the complement is. $)
    ufilss $p |- ( ( F e. UFil /\ S C_ X ) -> ( S e. F \/ ( X \ S ) e. F ) ) $=
      ( vs cufil wcel wss cdif wo cpw cvv wb cuni uniexg syl5eqel elpw2g syl
      cv wral wi cfil isufil simprbi wceq eleq1 difeq2 eleq1d orbi12d rcla4cv
      sylbird imp ) BFGZACHZABGZCAIZBGZJZUMUNACKZGZURUMCLGUTUNMUMBNLCBFODPACLQR
      UMESZBGZCVAIZBGZJZEUSTZUTURUAUMBUBGVFEBCDUCUDVEUREAUSVAAUEZVBUOVDUQVAABUF
      VGVCUPBVAACUGUHUIUJRUKUL $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d g F $.  $d g G $.  $d g X $.  $d g Y $.
    ufilmax.1 $e |- X = U. F $.
    ufilmax.2 $e |- Y = U. G $.
    $( Any filter finer than an ultrafilter is actually equal to it. $)
    ufilmax $p |- ( ( ( F e. UFil /\ G e. Fil ) /\ ( X = Y /\ F C_ G ) ) -> F =
    G ) $=
      ( vg cufil wcel cfil wceq wss wa cv cuni wi wral isufil2 simprbi unieq
      syl6eqr eqeq2d sseq2 anbi12d eqeq2 imbi12d rcla4cv syl imp31 ) AHIZBJIZCD
      KZABLZMZABKZUJCGNZOZKZAUPLZMZAUPKZPZGJQZUKUNUOPZPUJAJIVCGACERSVBVDGBJUPBK
      ZUTUNVAUOVEURULUSUMVEUQDCVEUQBODUPBTFUAUBUPBAUCUDUPBAUEUFUGUHUI $.
      $( [1-Dec-2009] $)
  $}

  ${
    $d x A $.  $d x B $.  $d x F $.  $d x X $.
    ufprim.1 $e |- X = U. F $.
    $( An ultrafilter is a prime filter. $)
    ufprim $p |- ( ( F e. UFil /\ A C_ X /\ B C_ X ) -> ( ( A e. F \/ B e. F )
    <-> ( A u. B ) e. F ) ) $=
      ( vx cufil wcel wss w3a wo cun wa cfil ufilfil 3ad2ant1 adantr simpr
      unss biimpi 3adant1 ssun1 a1i 3jca fillsb sylc ex ssun2 jaod wn cdif
      ufilss 3adant3 ord cin wi filint 3exp syl 3imp simp3 cv elin id imp
      eldifn sylan2 elun df-or bitri sylanb sylbi ssriv 3expia syld orrd
      impbid ) CGHZADIZBDIZJZACHZBCHZKZABLZCHZWAWBWFWCWAWBWFWAWBMZCNHZWBWEDIZAW
      EIZJWFWAWHWBVRVSWHVTCOZPZQWGWBWIWJWAWBRWAWIWBVSVTWIVRVSVTMWIABDSTUAZQWJWG
      ABUBUCUDAWECDEUEUFUGWAWCWFWAWCMZWHWCWIBWEIZJWFWAWHWCWLQWNWCWIWOWAWCRWAWIW
      CWMQWOWNBAUHUCUDBWECDEUEUFUGUIWAWFWDWAWFMZWBWCWPWBUJDAUKZCHZWCWPWBWRWAWBW
      RKZWFVRVSWSVTACDEULUMQUNWAWFWRWCWAWFWRJZWHWEWQUOZCHZVTXABIZJWCWAWFWHWRWLP
      WTXBVTXCWAWFWRXBVRVSWFWRXBUPUPZVTVRWHXDWKWHWFWRXBWEWQCUQURUSPUTWAWFVTWRVR
      VSVTVAPXCWTFXABFVBZXAHXEWEHZXEWQHZMXEBHZXEWEWQVCXEAHZUJZXHUPZXGXHXFXKXJXH
      XGXKXJXHXKVDVEXEDAVFVGXFXIXHKXKXEABVHXIXHVIVJVKVLVMUCUDXABCDEUEUFVNVOVPUG
      VQ $.
      $( [1-Jan-2010] $)
  $}

  ${
    $d a b f g h j k w x y z F $.  $d a b f g h j k w x y z X $.
    filssufil.1 $e |- X = U. F $.
    $( Lemma for ~ filssufil . $)
    filssufillem $p |- ( k e. Fil -> A. x ( ( x C_ ( { (/) } u. { g e. Fil | (
    U. k = U. g /\ k C_ g ) } ) /\ A. f e. x A. h e. x ( f C_ h \/ h C_ f ) )
    -> U. x e. ( { (/) } u. { g e. Fil | ( U. k = U. g /\ k C_ g ) } ) ) ) $=
      ( vj va vb vw vy vz cv cfil wcel c0 csn cuni wceq wss wa crab cun wo
      wral wi wn cin wne uncom sseq2i disjssun biimpcd necon3bd sylbi ad2antrl
      wex w3a df-3an unieq eqeq2d sseq2 anbi12d elrab simprr ad2antll elssuni
      sstrd uniss syl ssel elun elsn noel eleq2 mtbiri pm2.21d syl5bir jaoi
      syl6 adantr ad2antlr com23 imp3a 19.23adv eluni syl5ib r19.21aiv unissb
      sylibr eqssd wal eleq1 eqid filusb syl5cbir imp adantrr sseldd syl5cbi
      filesn con2d 19.21aiv alnex imnan albii notbii 3bitr4i sylib jctild
      ad2antrr adantrd a1i simprll simpll simplr eqtr2 ad2ant2l sseqtrd
      simprrr 3jca fillsb sylc exp32 jaod syld imp31 exp58 3impd 19.21aivv ex
      sseq1 orbi12d rcla42v filint 3exp a1i4 syl6com imp4c imp43 exp43 exp4c
      com24 adantl syl9r 3expib ancomsd imp32 a1i34 com34 com12 com15 imp42
      19.23advv anbi12i eeanv bitr4i r19.21aivv a1d 3jcad cvv wb visset uniex
      isfil ax-mp syl6ibr jcai sylanbrc 3com12d 3anass n0 elin exbii bitri
      uni0b orrd elsnc orbi12i bitr2i ) EOZPQZAOZRSZUWCTZCOZTZUAZUWCUWHUBZUCZCP
      UDZUEZUBZBOZDOZUBZUWQUWPUBZUFZDUWEUGBUWEUGZUCZUWETZUWNQZUHAUWDUXBUXDUWDUX
      BUCZUXCRUAZUXCPQZUWGUXCTZUAZUWCUXCUBZUCZUCZUFZUXDUXEUXFUXLUXEUWEUWFUBZUIZ
      UXLUXFUIUXEUXOUWEUWMUJZRUKZUXLUWOUXOUXQUHZUWDUXAUWOUWEUWMUWFUEZUBZUXRUWNU
      XSUWEUWFUWMULUMUXTUXNUXPRUXPRUAUXTUXNUWEUWMUWFUNUOUPUQURUXEIOZUWEQZUYAUWM
      QZUCZIUSZUXLUXQUXEUYDUXLIUXEUYDUXLUXEUYDUCZUXGUXIUXJUTUXLUYFUXIUXGUXJUXIU
      XGUXJUTUXIUXGUCUXJUYFUXIUXGUXJVAUYFUXIUXGUYFUWGUXHUYFUXJUWGUXHUBUYFUWCUYA
      UXCUYCUWCUYAUBZUXEUYBUYCUYAPQZUWGUYATZUAZUYGUCZUCZUYGUWLUYKCUYAPUWHUYAUAZ
      UWJUYJUWKUYGUYMUWIUYIUWGUWHUYAVBVCUWHUYAUWCVDVEVFZUYHUYJUYGVGUQVHUYBUYAUX
      CUBUXEUYCUYAUWEVIURZVJZUWCUXCVKVLUYFJOZUWGUBZJUXCUGUXHUWGUBUYFUYRJUXCUYFU
      YQKOZQZUYSUWEQZUCZKUSUYRUYQUXCQZUYFVUBUYRKUYFUYTVUAUYRUYFVUAUYTUYRUXBVUAU
      YTUYRUHZUHZUWDUYDUWOVUEUXAUWOVUAUYSUWNQZVUDUWEUWNUYSVMVUFUYSUWFQZUYSUWMQZ
      UFVUDUYSUWFUWMVNVUGVUDVUHVUGUYSRUAZVUDKRVOVUIUYTUYRVUIUYTUYQRQZUYQVPZUYSR
      UYQVQVRVSUQVUHUYSPQZUWGUYSTZUAZUWCUYSUBZUCZUCVUDUWLVUPCUYSPUWHUYSUAZUWJVU
      NUWKVUOVUQUWIVUMUWGUWHUYSVBVCUWHUYSUWCVDVEVFVUNVUDVULVUOVUNUYRUYQVUMUBUYT
      UWGVUMUYQVDUYQUYSVIVTURUQWAUQWBWCWDWEWFWGKUYQUWEWHWIWJJUXCUWGWKWLWMUYFUXI
      RUXCQZUIZUXHUXCQZUCZVUCUYSUXHUBZUYQUYSUBZUTUYSUXCQZUHZKWNJWNZUYQUYSUJZUXC
      QZKUXCUGJUXCUGZUTZUXGUYFUXIVVAVVFVVIUYFUXIVUTVUSUXIUWGUXCQVUTUYFUWGUXHUXC
      WOUYFUYAUXCUWGUYOUYCUWGUYAQZUXEUYBUYCUYLVVKUYNUYHUYJVVKUYGUYHUYJVVKUYJVVK
      UYIUYAQUYHUWGUYIUYAWOUYAUYIUYIWPWQWRWSWTUQVHXAXBUYFRLOZQZVVLUWEQZUIUHZLWN
      ZVUSUYFVVOLUYFVVNVVMUXBVVNVVMUIZUHZUWDUYDUWOVVRUXAUWOVVNVVLUWNQZVVQUWEUWN
      VVLVMZVVSVVLUWFQZVVLUWMQZUFZVVQVVLUWFUWMVNZVWAVVQVWBVWAVVLRUAZVVQLRVOZVWE
      VVMRRQRVPVVLRRVQVRUQVWBVVLPQZUWGVVLTZUAZUWCVVLUBZUCZUCZVVQUWLVWKCVVLPUWHV
      VLUAZUWJVWIUWKVWJVWMUWIVWHUWGUWHVVLVBVCUWHVVLUWCVDVEVFZVWGVVQVWKVVLXCWCUQ
      WAUQWBWCWDXDXEVVMVVNUCZUIZLWNVWOLUSZUIVVPVUSVWOLXFVVOVWPLVVMVVNXGXHVURVWQ
      LRUWEWHXIXJXKXLUYFUXIVVFUYFUXIUCZVVEJKVWRVUCVVBVVCVVDVWRUYQVVLQZVVNUCZLUS
      VVBVVCVVDUHUHZVUCVWRVWTVXALVWRVWSVVNVXAVWRVVNVWSVXAVWRVVNVWSVVBVVCVVDVWRV
      VNUCVWSVVBUCZVVCUCZUCVVLUXCUYSVVNVVLUXCUBVWRVXCVVLUWEVIWDVWRVVNVXCUYSVVLQ
      ZVWRVVNVVSVXCVXDUHZUXEVVNVVSUHZUYDUXIUWOVXFUWDUXAVVTURXMVWRVWCVXEVVSVWRVW
      AVXEVWBVWRVWEVXEVWAVWEVXEUHVWRVWEVXBVXDVVCVWEVWSVXDVVBVWEVWSVXDVWEVWSVUJV
      UKVVLRUYQVQVRVSXNXNXOVWFWIVWRVWLVXEVWBVWRVWLVXCVXDVWRVWLVXCUCUCZVWGVWSUYS
      VWHUBZVVCUTVXDVWRVWGVWKVXCXPVXGVWSVXHVVCVXCVWSVWRVWLVWSVVBVVCXQVHVXGUYSUX
      HVWHVXCVVBVWRVWLVWSVVBVVCXRVHVWRVWLUXHVWHUAZVXCUXIVWKVXIUYFVWGUXIVWIVXIVW
      JUWGUXHVWHXSWTXTWTYAVWRVWLVXBVVCYBYCUYQUYSVVLVWHVWHWPYDYEYFVWNWIYGVWDWIYH
      YIXAYJWEWFWGLUYQUWEWHWIYKYLYMUYFVVIUXIUYFVVHJKUXCUXCUYFUYQMOZQZVXJUWEQZUC
      ZUYSNOZQZVXNUWEQZUCZUCZNUSMUSZVVHVUCVVDUCZUYFVXRVVHMNUWDUWOUXAUYDVXRVVHUH
      ZUWDUXAUWOUYDVYAUHVXRUXAUWOUYDUWDVVHVXKVXLVXOVXPUXAUWOUYDUWDVVHUHUHZUHZUH
      ZVXLVXKVXOVXPVYDUHUHVXLVXPVXOVXKVYDVXLVXPVXOVXKVYDUHUHVXLVXPUCZUXAVXKVXOV
      YCVYEUXAVXJVXNUBZVXNVXJUBZUFZVXKVXOVYCUHZUHZUWTVYHVXJUWQUBZUWQVXJUBZUFBDV
      XJVXNUWEUWEUWPVXJUAUWRVYKUWSVYLUWPVXJUWQYNUWPVXJUWQVDYOUWQVXNUAVYKVYFVYLV
      YGUWQVXNVXJVDUWQVXNVXJYNYOYPVYEVYFVYJVYGVYFVXKUYQVXNQZVYEVYIVXJVXNUYQVMVX
      PVYMVYIUHVXLVXPUWOVXOVYMVYBVXPUWOVXOVYMVYBVXPUWOVXOUCVYMUCZUYDUWDVVHVXPVY
      NUCUYDUWDUCZUCVXNUXCVVGVXPVXNUXCUBVYNVYOVXNUWEVIXMVXPVYNUYDUWDVVGVXNQZVXP
      UWOVXOVYMUYDUWDVYPUHZUHZUWOVXPVXNUWNQZVXOVYMVYRUHZUHZUWEUWNVXNVMVYSVXNUWF
      QZVXNUWMQZUFWUAVXNUWFUWMVNWUBWUAWUCWUBVXNRUAZWUANRVOWUDVXOVYTWUDVXOUYSRQZ
      UYSVPZVXNRUYSVQVRVSUQWUCVXNPQZUWGVXNTZUAZUWCVXNUBZUCZUCZWUAUWLWUKCVXNPUWH
      VXNUAZUWJWUIUWKWUJWUMUWIWUHUWGUWHVXNVBVCUWHVXNUWCVDVEVFWULVXOVYMUYDVYQWUL
      VXOVYMUWDVYPWUGVXOVYMVYPUHUHWUKWUGVYMVXOVYPWUGVYMVXOVYPUYQUYSVXNYQYRWEWCY
      SYSUQWAUQYTUUAUUBXAUUCUUDUUEUUFUUGVYEVYGVXOVXKVYCVYGVXOUYSVXJQZVYEVXKVYCU
      HZVXNVXJUYSVMVXLWUNWUOUHVXPVXLWUNVXKUWOVYBVXLWUNVXKUCZUWOUCZUYDUWDVVHVXLW
      UQVVHVXLWUQUCVXJUXCVVGVXLVXJUXCUBWUQVXJUWEVIWCVXLWUPUWOVVGVXJQZVXLUWOWUPW
      URUWOVXLVXJUWNQZWUPWURUHZUWEUWNVXJVMWUSVXJUWFQZVXJUWMQZUFWUTVXJUWFUWMVNWV
      AWUTWVBWVAVXJRUAZWUTMRVOWVCWUNWURVXKWVCWUNWURWVCWUNWUEWUFVXJRUYSVQVRVSXNU
      QWVBVXJPQZUWGVXJTZUAZUWCVXJUBZUCZUCWUTUWLWVHCVXJPUWHVXJUAZUWJWVFUWKWVGWVI
      UWIWVEUWGUWHVXJVBVCUWHVXJUWCVDVEVFWVDWUTWVHWVDVXKWUNWURWVDVXKWUNWURUYQUYS
      VXJYQUUHUUIWCUQWAUQYTWEUUJXAYMUUKUUDWCUUGUULYGYHUUEYMUUEUUMUUBUUNWEUUOUUP
      VXTVXMMUSZVXQNUSZUCVXSVUCWVJVVDWVKMUYQUWEWHNUYSUWEWHUUQVXMVXQMNUURUUSWIUU
      TUVAUVBUXCUVCQUXGVVJUVDUWEAUVEUVFZJKUVCUXCUXHUXHWPUVGUVHUVIUVJUYPUVKUVLUX
      GUXIUXJUVMXKYMWGUXQUYAUXPQZIUSUYEIUXPUVNWVMUYDIUYAUWEUWMUVOUVPUVQWIYHUXFU
      XNUWEUVRXIWIUVSUXDUXCUWFQZUXCUWMQZUFUXMUXCUWFUWMVNWVNUXFWVOUXLUXCRWVLUVTU
      WLUXKCUXCPUWHUXCUAZUWJUXIUWKUXJWVPUWIUXHUWGUWHUXCVBVCUWHUXCUWCVDVEVFUWAUW
      BXKYMXE $.
      $( [10-Dec-2009] $)

    $( A filter is contained in some ultrafilter.  (Requires the Axiom of
       Choice.) $)
    filssufil $p |- ( F e. Fil -> E. f e. UFil ( X = U. f /\ F C_ f ) ) $=
      ( vh vg vk vx cfil wcel cv wpss wn c0 csn cuni wceq wss wa crab cun wral
      wrex cufil unieq syl6eqr eqeq1d sseq1 anbi12d rabbidv uneq2d raleqdv
      rexeqbidv wo wi wal filssufillem p0ex cpw visset uniex pwex rabss
      eqimss2 pwuni sstr mpan sspwb elpw 3imtr4i syl adantr a1i mprgbir ssexi
      unex zorn vtoclga ssun2 eqid ssid pm3.2i jctr eqeq2d sseq2 elrab sylibr
      sseldd psseq2 notbid rcla4v psseq1 wne filusb ne0i 0pss syl5cbir nsyld
      con2d imp pm2.21d ex elsn syl5ib isufil2 simplrl simprl eqtr adantlr
      adantll ad2ant2l adantrrr adantrl jca32 df-pss simplbi2 necon1bd adantl
      ad2antll imim2d mpid expr com23 ralimdv2 sylanbrc simplrr jca exp31 jaod
      elun imp3a reximdv2 mpd ) BIJZAKZEKZLZMZENOZCFKZPZQZBYTRZSZFITZUAZUBZAUUF
      UCZCYOPZQZBYORZSZAUDUCYREYSGKZPZUUAQZUUMYTRZSZFITZUAZUBZAUUSUCZUUHGBIUUMB
      QZUUTUUGAUUSUUFUVBUURUUEYSUVBUUQUUDFIUVBUUOUUBUUPUUCUVBUUNCUUAUVBUUNBPZCU
      UMBUEDUFUGUUMBYTUHUIUJUKZUVBYREUUSUUFUVDULUMUUMIJHKZUUSRYOYPRZYPYORUNEUVE
      UBAUVEUBSUVEPUUSJUOHUPUVAHAFEGBCDUQAEHUUSYSUURURUURUUNUSZUSZUVGUUNUUMGUTV
      AVBVBUURUVHRUUQYTUVHJZUOZFIUUQFIUVHVCUVJYTIJUUOUVIUUPUUOUUAUUNRZUVIUUAUUN
      VDUUAUSZUVGRZYTUVGRZUVKUVIYTUVLRUVMUVNYTVEYTUVLUVGVFVGUUAUUNVHYTUVGFUTVIV
      JVKVLVMVNVOVPVQVKVRYNUUGUULAUUFUDYNYOUUFJZUUGYOUDJZUULSZYNYOYSJZYOUUEJZUN
      UUGUVQUOZUVOYNUVRUVTUVSYNYONQZUVTUVRYNUWAUVTYNUWASUUGUVQYNUWAUUGMYNUUGUWA
      YNUUGYOBLZUWAYNBUUFJUUGUWBMZUOYNUUEUUFBUUEUUFRZYNUUEYSVSZVMYNYNCCQZBBRZSZ
      SBUUEJYNUWHUWFUWGCVTBWAWBWCUUDUWHFBIYTBQZUUBUWFUUCUWGUWIUUACCUWIUUAUVCCYT
      BUEDUFWDYTBBWEUIWFWGWHYRUWCEBUUFYPBQYQUWBYPBYOWIWJWKVKUWAUWBNBLZYNYONBWLY
      NBNWMZUWJYNCBJUWKBCDWNBCWOVKBWPWGWQWRWSWTXAXBANXCXDYNYOIJZUULSZUVTUVSYNUW
      MUUGUVQYNUWMSZUUGSZUVPUULUVPUWLUUIYPPZQZUVFSZYOYPQZUOZEIUBZUWOEYOUUIUUIVT
      XEYNUWLUULUUGXFUWNUUGUXAUWNYRUWTEUUFIUWNYPIJZYPUUFJZYRUOZUWTUWNUXBUXDUWTU
      OUWNUXBSUWRUXDUWSUWNUXBUWRUXDUWSUOUWNUXBUWRSSZUXDUXCUWSUXEUUEUUFYPUWDUXEU
      WEVMUXEUXBCUWPQZBYPRZSZSYPUUEJUXEUXBUXFUXGUWNUXBUWRXGUWNUXBUWQUXFUVFUWMUW
      QUXFYNUXBUULUWQUXFUWLUUJUWQUXFUUKCUUIUWPXHXIXJXKXLUWNUWRUXGUXBUWMUVFUXGYN
      UWQUULUVFUXGUWLUUKUVFUXGUUJBYOYPVFXJXJXKXMXNUUDUXHFYPIYTYPQZUUBUXFUUCUXGU
      XIUUAUWPCYTYPUEWDYTYPBWEUIWFWGWHUXEYRUWSUXCUWRYRUWSUOZUWNUXBUVFUXJUWQUVFY
      QYOYPYQUVFYOYPWMYOYPXOXPXQXRXSXTYAYBYCXBYCYDWTYEYNUWLUULUUGYFYGYHUUDUULFY
      OIYTYOQZUUBUUJUUCUUKUXKUUAUUICYTYOUEWDYTYOBWEUIWFXDYIYOYSUUEYJXDYKYLYM $.
      $( [2-Dec-2009] $)
  $}

  ${
    $d f g s u v x y F $.  $d f g s u v x y X $.
    ufileu.1 $e |- X = U. F $.
    $( Lemma for ~ ufileu . $)
    ufileulem $p |- ( ( ( F e. Fil /\ s C_ X ) /\ ( -. s e. F /\ -. ( X \ s )
    e. F ) ) -> ( fi ` ( F u. { s } ) ) e. fBas ) $=
      ( vx vy cfil wcel cv wss wa wn cdif csn cun cfi cfv cfbas c0 wne wnel wb
      cvv snex unexg mpan2 fsubbas syl ad2antrr filusb ne0i ssun1 ssn0 mpan
      3syl cin wral wceq wi ineq2 neeq1d imbi2d cuni elssuni syl6ssr reldisj
      adantl difss fillsb 3expd imp mpi adantlr sylbid necon3bd ex com23
      adantrl syl5cbir elsn syl7ib imp3a r19.21aivv filfbas visset a1i difeq2
      eleq1d dif0 syl5eqel adantr oefil2 syl11anc fbunfip mpbird mpbir2and ) AG
      HZCIZBJZKZWRAHLZBWRMZAHZLZKZKZAWRNZOZPQZRHZXHSTZSXIUAZWQXJXKXLKUBZWSXEWQX
      HUCHZXMWQXGUCHXNWRUDAXGGUCUEUFXHUCUGUHUIWQXKWSXEWQBAHASTZXKABDUJZABUKAXHJ
      XOXKAXGULAXHUMUNUOUIXFXLEIZFIZUPZSTZFXGUQEAUQZXFXTEFAXGXFXQAHZXRXGHZXTXFY
      BXRWRURZXTYCXFYDYBXTYDYBXTUSYBXQWRUPZSTZUSZXFYDXTYFYBYDXSYESXRWRXQUTVAVBW
      TXDYGXAWTXDYGWTYBXDYFWTYBXDYFUSWTYBKZXCYESYHYESURZXQXBJZXCYBYIYJUBZWTYBXQ
      BJYKYBXQAVCBXQAVDDVEXQWRBVFUHVGWQYBYJXCUSZWSWQYBKXBBJZYLBWRVHWQYBYMYLUSWQ
      YBYMYJXCXQXBABDVIVJVKVLVMVNVOVPVQVKVRVSVQFWRVTWAWBWCXFARHZXGRHZXLYAUBWQYN
      WSXEAWDUIXFXGGHZYOXFWRUCHZWRSTZYPYQXFCWEWFWTXDYRXAWTXDYRWTXCWRSWQWRSURZXC
      USWSYSXCBSMZAHWQYSXBYTAWRSBWGWHWQBAYTXPBWIWJVSWKVOVKVRWRUCWLWMXGWDUHEFAXG
      WNWMWOWP $.
      $( [15-Dec-2009] $)

    $( If the ultrafilter containing a given filter is unique, the filter is an
       ultrafilter. $)
    ufileu $p |- ( F e. Fil ->
        ( F e. UFil <-> E! f e. UFil ( X = U. f /\ F C_ f ) ) ) $=
      ( vg vs vu vv vx vy cfil wcel cufil cv cuni wceq wss wa wreu wb wral
      wrex wi eqid ufilmax eqcomd ex ufilfil sylan2 ssid pm3.2i unieq syl6eqr
      eqeq2d sseq2 anbi12d mpbiri impbid1 r19.21aiva eqeq2 bibi2d ralbidv
      rcla4ev mpdan reu6 sylibr cdif wo cpw weu wn wmo wex csn cun cfi cfv cfg
      cfbas ufileulem fgfil filssufil 3syl c0 wne wnel cvv snex unexg mpan2
      fsubbas syl ad2antrr filusb ne0i ssun1 ssn0 mpan cin ineq2 neeq1d imbi2d
      elssuni syl6ssr reldisj adantl dfss4 biimpi sseq2d ad2antlr bitrd fillsb
      3expd com23 imp31 sylbid necon3bd imp adantrr syl5cbir elsn syl5ib imp3a
      r19.21aivv filfbas uniexg syl5eqel difexg difeq2 sylan9eq dif0 syl6eq
      eqeltrd oefil2 syl11anc fbunfip mpbird mpbir2and simplrl simplr ssequn2
      sylib visset unisn eqcomi uneq12i syl5reqr uniun fiuni fgbas 3eqtrd
      simprll eqtrd a1i abfi2 sstrd fbssfg simprlr jca32 simplrr unisng
      uneq12d difss mpbi syl5eqr simprrl simprrr filesn adantr ad2antrl ssun2
      simprr snid sseldd adantlrr snidg adantlrl adantrl simpr eleqtrrd filint
      syl111anc difdisj syl5eqelr mtand eleq1 anbi1d eqeq1 notbid anbi2d
      sylan9bb cla42ev syl21anc exp31 r19.23advv reeanv syl5ibr mp2and elpw
      r19.23adv rexnal ioran rexbii bitr3i wal mo4 notbii exnal annim exbii
      bitr2i 3bitr2i 3imtr4g eumo nsyld con4d df-reu isufil baibr sylibd
      impbid2 ) BKLZBMLZCANZOZPZBUYDQZRZAMSZUYCUYHUYDENZPZTZAMUAZEMUBZUYIUYCUYH
      UYDBPZTZAMUAZUYNUYCUYPAMUYCUYDMLZRUYHUYOUYCUYDKLZUYHUYOUCUYRUYCUYSRZUYHUY
      OUYTUYHRBUYDBUYDCUYEDUYEUDUEUFUGUYDUHUIUYOUYHCCPZBBQZRVUAVUBCUDBUJUKUYOUY
      FVUAUYGVUBUYOUYECCUYOUYEBOZCUYDBULDUMUNUYDBBUOUPUQURUSUYMUYQEBMUYJBPZUYLU
      YPAMVUDUYKUYOUYHUYJBUYDUTVAVBVCVDUYHAEMVEVFUYBUYIFNZBLZCVUEVGZBLZVHZFCVIZ
      UAZUYCUYBUYRUYHRZAVJZVUKUYIUYBVUKVUMUYBVUKVKZVULAVLZVUMUYBVUFVKZVUHVKZRZF
      VUJUBZVULUYJMLZCUYJOZPZBUYJQZRZRZRZUYKVKZRZEVMZAVMZVUNVUOVKZUYBVURVVJFVUJ
      UYBVUECQZVURVVJUCVUEVUJLUYBVVLVURVVJUYBVVLRZVURRZBVUEVNZVOZVPVQZVRVQZOZGN
      ZOZPZVVRVVTQZRZGMUBZBVUGVNZVOZVPVQZVRVQZOZHNZOZPZVWIVWKQZRZHMUBZVVJVVNVVQ
      VSLZVVRKLVWEBCFDVTZVVQWAGVVRVVSVVSUDWBWCVVNVWHVSLZVWIKLVWPVVNVWSVWGWDWEZW
      DVWHWFZUYBVWSVWTVXARTZVVLVURUYBVWGWGLZVXBUYBVWFWGLVXCVUGWHBVWFKWGWIWJZVWG
      WGWKWLWMUYBVWTVVLVURUYBCBLZBWDWEZVWTBCDWNZBCWOBVWGQZVXFVWTBVWFWPZBVWGWQWR
      WCWMVVNVXAINZJNZWSZWDWEZJVWFUAIBUAZVVNVXMIJBVWFVVNVXJBLZVXKVWFLZVXMVVNVXP
      VXOVXMVVNVXKVUGPZVXOVXMUCZVXPVXQVXRVXOVXJVUGWSZWDWEZUCZVVNVXQVXMVXTVXOVXQ
      VXLVXSWDVXKVUGVXJWTXAXBVVMVUPVYAVUQVVMVUPVYAVVMVXOVUPVXTVVMVXOVUPVXTUCVVM
      VXORZVUFVXSWDVYBVXSWDPZVXJVUEQZVUFVYBVYCVXJCVUGVGZQZVYDVXOVYCVYFTZVVMVXOV
      XJCQVYGVXOVXJVUCCVXJBXCDXDVXJVUGCXEWLXFVVLVYFVYDTUYBVXOVVLVYEVUEVXJVVLVYE
      VUEPVUECXGXHZXIXJXKUYBVVLVXOVYDVUFUCZUYBVXOVVLVYIUYBVXOVVLVYDVUFVXJVUEBCD
      XLXMXNXOXPXQUGXNXRXSXTJVUGYAYBXNYCYDVVNBVSLZVWFVSLZVXAVXNTUYBVYJVVLVURBYE
      WMVVNVWFKLZVYKVVNVUGWGLZVUGWDWEZVYLUYBVYMVVLVURUYBCWGLZVYMUYBVUCWGCBKYFDY
      GZCVUEWGYHZWLWMVVMVUPVYNVUQVVMVUPVYNVVMVUFVUGWDVVMVUGWDPZVUFVVMVYRRZVUECB
      VYSVUECWDVGZCVVMVYRVUEVYEVYTVVLVUEVYEPUYBVVLVYEVUEVYHUFXFVUGWDCYIYJCYKYLU
      YBVXEVVLVYRVXGWMYMUGXQXRXSVUGWGYNYOVWFYEWLIJBVWFYPYOYQYRZVWHWAHVWIVWJVWJU
      DWBWCVVNVWDVWORZHMUBGMUBVVJVWEVWPRVVNWUBVVJGHMMVVNVVTMLZVWKMLZRZWUBVVJVVN
      WUERZWUBRZWUCCVWAPZBVVTQZRZRZWUDCVWLPZBVWKQZRZRZVVTVWKPZVKZVVJWUGWUCWUHWU
      IVVNWUCWUDWUBYSWUGCVVSVWAVVNCVVSPWUEWUBVVNCVVPOZVVQOZVVSVVNCVUCVVOOZVOZWU
      RVVNCVUEVOZCWVAVVNVVLWVBCPUYBVVLVURYTVUECUUAUUBCVUCVUEWUTDWUTVUEVUEFUUCZU
      UDUUEUUFUUGBVVOUUHUMVVNVVPWGLZWURWUSPUYBWVDVVLVURUYBVVOWGLWVDVUEWHBVVOKWG
      WIWJZWMVVPWGUUIWLVVNVWQWUSVVSPVWRVVQWUSWUSUDUUJWLUUKWMWUFVWBVWCVWOUULUUMW
      UGBVVRVVTVVNBVVRQWUEWUBVVNBVVQVVRVVNBVVPVVQBVVPQVVNBVVOWPUUNUYBVVPVVQQZVV
      LVURUYBWVDWVFWVEVVPWGUUOWLWMZUUPVVNVWQVVQVVRQVWRVVQUUQWLZUUPWMWUFVWBVWCVW
      OUURUUPUUSWUGWUDWULWUMVVNWUCWUDWUBUUTWUGCVWJVWLVVNCVWJPWUEWUBVVNCVWHOZVWJ
      UYBCWVIPVVLVURUYBCVWGOZWVIUYBCVUCVWFOZVOZWVJUYBCVUGVOZWVLCUYBCVUCVUGWVKCV
      UCPUYBDUUNUYBWVKVUGUYBVYOVYMWVKVUGPVYPVYQVUGWGUVAWCUFUVBVUGCQWVMCPCVUEUVC
      VUGCUUAUVDUVEBVWFUUHUMUYBVXCWVJWVIPVXDVWGWGUUIWLUUMWMVVNVWSWVIVWJPWUAVWHW
      VIWVIUDUUJWLUUMWMWUFVWDVWMVWNUVFUUMWUGBVWIVWKVVNBVWIQWUEWUBVVNBVWHVWIVVNB
      VWGVWHVXHVVNVXIUUNUYBVWGVWHQZVVLVURUYBVXCWVNVXDVWGWGUUOWLWMZUUPVVNVWSVWHV
      WIQWUAVWHUUQWLZUUPWMWUFVWDVWMVWNUVGUUPUUSWUGWUPWDVVTLZWUEWVQVKZVVNWUBWUCW
      VRWUDWUCVVTKLZWVRVVTUHZVVTUVHWLUVIXJWUGWUPRZVUEVUGWSZVVTWDWWAWVSVUEVVTLZV
      UGVVTLWWBVVTLWUFWVSWUBWUPWUCWVSVVNWUDWVTUVJWMWUGWWCWUPWUFVWDWWCVWOVVNWUCV
      WDWWCWUDVVNWUCRZVWDRZVVOVVTVUEWWEVVOVVRVVTVVNVVOVVRQWUCVWDVVNVVOVVQVVRVVN
      VVOVVPVVQVVOVVPQVVNVVOBUVKUUNWVGUUPWVHUUPWMWWDVWBVWCUVLUUPVUEVVOLWWEVUEWV
      CUVMUUNUVNUVOXSUVIWWAVUGVWKVVTWUGVUGVWKLZWUPWUFVWOWWFVWDVVNWUDVWOWWFWUCVV
      NWUDRZVWORVWIVWKVUGWWGVWMVWNUVLVVNVUGVWILWUDVWOVVNVWFVWIVUGVVNVWFVWHVWIVV
      NVWFVWGVWHVWFVWGQVVNVWFBUVKUUNWVOUUPWVPUUPUYBVUGVWFLZVVLVURUYBVYOVYMWWHVY
      PVYQVUGWGUVPWCWMUVNWMUVNUVQUVRUVIWUGWUPUVSUVTVUEVUGVVTUWAUWBVUECUWCUWDUWE
      VVHWUKWUORZWUQRZAEVVTVWKGUUCHUUCUYDVVTPZVVHWUKVVERZVVTUYJPZVKZRUYJVWKPZWW
      JWWKVVFWWLVVGWWNWWKVULWUKVVEWWKUYRWUCUYHWUJUYDVVTMUWFWWKUYFWUHUYGWUIWWKUY
      EVWACUYDVVTULUNUYDVVTBUOUPUPUWGWWKUYKWWMUYDVVTUYJUWHUWIUPWWOWWLWWIWWNWUQW
      WOVVEWUOWUKWWOVUTWUDVVDWUNUYJVWKMUWFWWOVVBWULVVCWUMWWOVVAVWLCUYJVWKULUNUY
      JVWKBUOUPUPUWJWWOWWMWUPUYJVWKVVTUTUWIUPUWKUWLUWMUWNUWOVWDVWOGHMMUWPUWQUWR
      UWNVUECWVCUWSYBUWTVUNVUIVKZFVUJUBVUSVUIFVUJUXAWWPVURFVUJVUFVUHUXBUXCUXDVV
      KVVFUYKUCZEUXEZAUXEZVKWWRVKZAVMVVJVUOWWSVULVVEAEUYKUYRVUTUYHVVDUYDUYJMUWF
      UYKUYFVVBUYGVVCUYKUYEVVACUYDUYJULUNUYDUYJBUOUPUPUXFUXGWWRAUXHWWTVVIAVVIWW
      QVKZEVMWWTVVHWXAEVVFUYKUXIUXJWWQEUXHUXKUXJUXLUXMVUMVUOUCUYBVULAUXNUUNUXOU
      XPUYHAMUXQYBUYCUYBVUKFBCDUXRUXSUXTUYA $.
      $( [3-Dec-2009] $)
  $}

  ${
    $d f x y z F $.  $d f x y z X $.
    filufint.1 $e |- X = U. F $.
    $( A filter is equal to the intersection of the ultrafilters containing
       it. $)
    filufint $p |- ( F e. Fil -> |^| { f e. UFil | ( X = U. f /\ F C_ f ) } = F
    ) $=
      ( vx vy vz cfil wcel cv cuni wceq wss wa cufil crab cint wi wral wn wrex
      w3a cdif csn cun cfi cfv cfg cfbas c0 wne wnel wb cvv snex unexg mpan2
      fsubbas syl 3ad2ant1 filusb ne0i ssun1 ssn0 mpan 3syl cin ineq2 neeq1d
      elssuni syl6ssr reldisj 3ad2ant2 dfss4 biimpi sseq2d 3ad2ant3 bitrd
      fillsb 3expd 3imp sylbid necon3bd 3exp com24 3imp1 syl5cbir expimpd
      elsni sylan2i r19.21aivv filfbas uniexg syl5eqel difexg difeq2 dif0
      syl6eq eqtr3d eqeltrd 3expia ex com23 oefil2 syl11anc fbunfip mpbird
      mpbir2and fgfil a1i unisng eqcomd uneq12d difss ssequn2 mpbi syl5eqr
      uniun syl6eqr fiuni eqid fgbas 3eqtrd simpl2 simprl eqtrd abfi2 sstrd
      fbssfg adantr simprr ufilfil filesn simpr3 ssun2 simpr2 snidg sseldd
      filint syl111anc difdisj syl5eqelr 3exp2 imp32 mtod jca31 reximdvai
      filssufil syl5 imp3a mp2and sseq2 syl5bir con3d com12 adantrd ancld
      reximdv syl5com pm2.61d rexanali syl6ib con4d visset elintrab syl5ib
      ssrdv wal 19.21aiv cab df-rab inteqi sseq2i ssintab bitr2i sylib eqssd )
      BHIZCAJZKZLZBUVSMZNZAOPZQZBUVREUWEBUVRUWCEJZUVSIZRAOSZUWFBIZUWFUWEIUVRUWI
      UWHUVRUWITZUWCUWGTZNZAOUAZUWHTUVRUWJUWMUVRUWJNUWFCMZUWMUVRUWJUWNUWMUVRUWJ
      UWNUBZBCUWFUCZUDZUEZUFUGZUHUGZHIZCUWTKZLZUWMUWOUWSUIIZUXAUWOUXDUWRUJUKZUJ
      UWSULZUVRUWJUXDUXEUXFNUMZUWNUVRUWRUNIZUXGUVRUWQUNIUXHUWPUOBUWQHUNUPUQZUWR
      UNURUSUTUVRUWJUXEUWNUVRCBIZBUJUKZUXEBCDVAZBCVBBUWRMZUXKUXEBUWQVCZBUWRVDVE
      VFUTUWOUXFFJZGJZVGZUJUKZGUWQSFBSZUWOUXRFGBUWQUWOUXOBIZUXPUWPLZUXRUXPUWQIU
      WOUXTUYAUXRUYAUXRUXOUWPVGZUJUKZUWOUXTNUYAUXQUYBUJUXPUWPUXOVHVIUVRUWJUWNUX
      TUYCUVRUXTUWNUWJUYCUVRUXTUWNUWJUYCRUVRUXTUWNUBZUWIUYBUJUYDUYBUJLZUXOUWFMZ
      UWIUYDUYEUXOCUWPUCZMZUYFUXTUVRUYEUYHUMZUWNUXTUXOCMUYIUXTUXOBKZCUXOBVJDVKU
      XOUWPCVLUSVMUWNUVRUYHUYFUMUXTUWNUYGUWFUXOUWNUYGUWFLZUWFCVNVOZVPVQVRUVRUXT
      UWNUYFUWIRUVRUXTUWNUYFUWIUXOUWFBCDVSVTWAWBWCWDWEWFWGWHUXPUWPWIWJWKUWOBUII
      ZUWQUIIZUXFUXSUMUVRUWJUYMUWNBWLUTUWOUWQHIZUYNUWOUWPUNIZUWPUJUKZUYOUVRUWJU
      YPUWNUVRCUNIZUYPUVRUYJUNCBHWMDWNZCUWFUNWOZUSUTUVRUWJUWNUYQUVRUWNUWJUYQUVR
      UWNUWJUYQRUVRUWNNUWIUWPUJUVRUWNUWPUJLZUWIUVRUWNVUAUBZUWFCBVUBUYGUWFCUWNUV
      RUYKVUAUYLVMVUAUVRUYGCLUWNVUAUYGCUJUCCUWPUJCWPCWQWRVQWSUVRUWNUXJVUAUXLUTW
      TXAWCXBXCWAUWPUNXDXEUWQWLUSFGBUWQXFXEXGXHZUWSXIUSUWOCUWRKZUWSKZUXBUWOCUYJ
      UWQKZUEZVUDUWOCUWPUEZVUGCUWOCUYJUWPVUFCUYJLUWODXJUVRUWJUWPVUFLUWNUVRVUFUW
      PUVRUYRUYPVUFUWPLUYSUYTUWPUNXKVFXLUTXMUWPCMVUHCLCUWFXNUWPCXOXPXQBUWQXRXSU
      VRUWJVUDVUELZUWNUVRUXHVUIUXIUWRUNXTUSUTUWOUXDVUEUXBLVUCUWSVUEVUEYAYBUSYCU
      WOUXAUXCUWMUWOUXCUXAUWMUWOUXCUXAUWMRUWOUXCNZUXBUVTLZUWTUVSMZNZAOUAUWMUXAV
      UJVUMUWLAOUWOUXCUVSOIZVUMUWLRUWOUXCVUNUBZVUMUWLVUOVUMNZUWAUWBUWKVUPCUXBUV
      TUWOUXCVUNVUMYDVUOVUKVULYEYFVUPBUWTUVSVUOBUWTMZVUMUWOUXCVUQVUNUWOBUWSUWTU
      WOBUWRUWSUXMUWOUXNXJUVRUWJUWRUWSMZUWNUVRUXHVURUXIUWRUNYGUSZUTYHUWOUXDUWSU
      WTMZVUCUWSYIZUSYHUTYJVUOVUKVULYKYHVUPUWGUJUVSIZVUOVVBTZVUMVUNUWOVVCUXCVUN
      UVSHIZVVCUVSYLZUVSYMUSVQYJVUOVUKVULUWGVVBRVUOVUKVULUWGVVBVUOVUKVULUWGUBZN
      ZUWFUWPVGZUVSUJVVGVVDUWGUWPUVSIVVHUVSIVUOVVDVVFVUNUWOVVDUXCVVEVQYJVUOVUKV
      ULUWGYNVVGUWQUVSUWPVVGUWQUWTUVSVUOUWQUWTMVVFVUOUWQUWSUWTUWOUXCUWQUWSMZVUN
      UVRUWJVVIUWNUVRUWQUWRUWSUWQUWRMUVRUWQBYOXJVUSYHUTUTVUOUXDVUTUWOUXCUXDVUNV
      UCUTVVAUSYHYJVUOVUKVULUWGYPYHVUOUWPUWQIZVVFUWOUXCVVJVUNUVRUWJVVJUWNUVRUYR
      UYPVVJUYSUYTUWPUNYQVFUTUTYJYRUWFUWPUVSYSYTUWFCUUAUUBUUCUUDUUEUUFXBXAUUGAU
      WTUXBUXBYAUUHUUIXBXCUUJUUKXAUVRUWNTZUWMRUWJVVKUWCAOUAUWMUVRVVKUWCUWLAOVVK
      UWCUWKVVKUWAUWKUWBUWAVVKUWKUWAUWGUWNUWAUWNUWFUVTMUWGCUVTUWFUULUWFUVSVJUUM
      UUNUUOUUPUUQUURABCDUUHUUSYJUUTXBUWCUWGAOUVAUVBUVCUWCAUWFOEUVDUVEUVFUVGUVR
      VUNUWCNZUWBRZAUVHZBUWEMZUVRVVMAVVMUVRVUNUWAUWBYKXJUVIVVOBVVLAUVJZQZMVVNUW
      EVVQBUWDVVPUWCAOUVKUVLUVMVVLABUVNUVOUVPUVQ $.
      $( [1-Jan-2010] $)
  $}

  ${
    $d w x y z F $.  $d w x y z X $.
    uffixfr.1 $e |- X = U. F $.
    $( An ultrafilter is either fixed or free.  A fixed ultrafilter is called
       principal, and a free ultrafilter is called nonprincipal.  Note that
       free ultrafilters cannot be constructed. $)
    uffixfr $p |- ( F e. UFil -> ( |^| F = (/) \/ E. x e. X A. y e. ~P X ( y e.
    F <-> x e. y ) ) ) $=
      ( vz vw cufil wcel cint c0 wceq cv wb cpw wral wrex wex wn wa wne r19.2z
      cuni elssuni syl6ssr sseld r19.23aiv syl cfil ufilfil filusb ne0i 3syl
      sylan visset elint2 sylan2b wss wi elinti ad2antlr csn cun cfg cfv cfbas
      cvv snex a1i snnzg ad2antll oefil2 filfbas syl11anc snssd adantr unisn
      eqcomi extbas1 uniun elfg simpr uniexg syl5eqel unisng sseqtr4d ssun2
      sstrd ad2ant2r ssun1 snid sselii snssi sseq1 rcla4ev mpbir2and simpll
      fgfil ad2antrr uneq12d fgbas ssequn1 sylib 3eqtr3rd adantl adantlr imp
      adantll ex ssrdv eqid ufilmax syl22anc eleqtrrd expr impbid elpw syl5ib
      r19.21aiv eleq1 bibi2d ralbidv 19.23adv neq0 orrd ) CHIZCJZKLZBMZCIZAMZYI
      IZNZBDOZPZADQZYFFMZYGIZFRYPYHSYFYRYPFYFYRYPYFYRTZYQDIZYJYQYIIZNZBYNPZYPYF
      YQYKIZACPZYTYRCKUAZUUEYTYFUUFUUETUUDACQYTUUDACUBUUDYTACYKCIZYKDYQUUGYKCUC
      ZDYKCUDEUEZUFUGUHYFCUIIDCIUUFCUJCDEUKCDULUMUNAYQCFUOUPUQZYSUUBBYNYSYIDURZ
      UUBYIYNIYSUUKUUBYSUUKTYJUUAYRYJUUAUSYFUUKYQCYIUTVAYSUUKUUAYJYSUUKUUATZTZY
      IYQVBZVBZDVBZVCZVDVEZCUUMYIUURIZYIUUOUCZUUPUCZVCZURZYKYIURZAUUQQZUUMUUQVF
      IZUUSUVCUVETNUUMUUOVFIZUUNDURZUVFUUMUUNVGIZUUNKUAZUVGUVIUUMYQVHZVIUUAUVJY
      SUUKYQYIVJVKUVIUVJTUUOUIIUVGUUNVGVLUUOVMUHVNYSUVHUULYSYQDUUJVOVPZDUUOUUNU
      UTUUNUUNUVKVQZVRVSVNZAYIUUQUVBUUQUCUVBUUOUUPVTVRZWAUHYFUUKUVCYRUUAYFUUKTZ
      YIUVAUVBUVPYIDUVAYFUUKWBYFUVADLZUUKYFDVGIUVQYFUUHVGDCHWCEWDDVGWEUHZVPWFUV
      AUVBURZUVPUVAUUTWGZVIWHWIUUMUUNUUQIZUUNYIURZUVEUWAUUMUUOUUQUUNUUOUUPWJUUN
      UVKWKWLZVIUUAUWBYSUUKYQYIWMVKUVDUWBAUUNUUQYKUUNYIWNWOVNWPUUMYFUURUIIZDUUR
      UCZLCUURURCUURLYFYRUULWQUUMUVFUWDUVNUUQWRUHUUMUVBUUNDVCZUWEDUUMUUTUUNUVAD
      UUTUUNLUUMUVMVIYFUVQYRUULUVRWSWTUUMUVFUVBUWELUVNUUQUVBUVOXAUHUUMUVHUWFDLU
      VLUUNDXBXCXDUUMACUURUUMUUGYKUURIZUUMUUGTUWGYKUVBURZGMZYKURZGUUQQZUUMUWGUW
      HUWKTNZUUGUUMUVFUWLUVNGYKUUQUVBUVOWAUHVPYSUUGUWHUULYFUUGUWHYRYFUUGTZYKUVA
      UVBUWMYKDUVAUUGYKDURYFUUIXEYFUVQUUGUVRVPWFUVSUWMUVTVIWHXFXFYSUUGUWKUULYRU
      UGUWKYFYRUUGTZUWAUUNYKURZUWKUWAUWNUWCVIUWNYQYKYRUUGUUDYQCYKUTXGVOUWJUWOGU
      UNUUQUWIUUNYKWNWOVNXHXFWPXIXJCUURDUWEEUWEXKXLXMXNXOXPXIYIDBUOXQXRXSYOUUCA
      YQDYKYQLZYMUUBBYNUWPYLUUAYJYKYQYIXTYAYBWOVNXIYCFYGYDXRYE $.
      $( [4-Dec-2009] $)
  $}

  ${
    $d x y A $.  $d x y B $.  $d x y X $.
    $( The condition describing a fixed ultrafilter always produces an
       ultrafilter. $)
    fixufil $p |- ( ( X e. B /\ A e. X ) -> { x e. ~P X | A e. x } e. UFil ) $=
      ( vy wcel wa cv cpw crab cfil cuni cdif wo wral cufil wceq wss csn cab
      c0 wne simpl snssi adantl snnzg supfil syl111anc wb visset elpw a1i
      snssg anbi12d abbidv df-rab syl5eq eleq1 unieq eqeq2d syl mpbird simprl
      wi wn pm2.27 eleq2 elrab difss elpw2g mpbiri ad2antrr eldif biimpri
      ad2ant2l sylanbrc expr syld anbi1i bitri notbii imnan bitr4i syl5ib orrd
      ex adantr sseq2 ad2antll difeq1 eleq1d orbi2d 3imtr3d r19.21aiv jca
      mpdan eqid isufil sylibr ) DCFZBDFZGZBAHZFZADIZJZKFZEHZXFFZXFLZXHMZXFFZNZ
      EXJIZOZGZXFPFXBXGDXJQZGZXPXBXRXCDRZBSZXCRZGZATZKFZDYCLZQZGZXBWTXTDRZXTUAU
      BZYGWTXAUCXAYHWTBDUDUEXAYIWTBDUFUEADXTCUGUHXBXFYCQZXRYGUIXBXCXEFZXDGZATYC
      XFXBYLYBAXBYKXSXDYAYKXSUIXBXCDAUJUKULXAXDYAUIWTBXCDUMUEUNUOXDAXEUPUQYJXGY
      DXQYFXFYCKURYJXJYEDXFYCUSUTUNVAVBXBXRGZXGXOXBXGXQVCYMXMEXNYMXHXJRZXMXHXNF
      YMXHDRZXIDXHMZXFFZNZYNXMXBYOYRVDXRXBYOYRXBYOGZXIYQYSYOBXHFZVEZVDZYQXIVEZY
      SUUBUUAYQYOUUBUUAVDXBYOUUAVFUEXBYOUUAYQYQYPXEFZBYPFZXBYOUUAGZGXDUUEAYPXEX
      CYPBVGVHWTUUDXAUUFWTUUDYPDRDXHVIYPDCVJVKVLXAUUAUUEWTYOUUEXAUUAGBDXHVMVNVO
      VPVQVRUUCYOYTGZVEUUBXIUUGXIXHXEFZYTGUUGXDYTAXHXEXCXHBVGVHUUHYOYTXHDEUJZUK
      VSVTWAYOYTWBWCWDWEWFWGXQYOYNUIXBXGDXJXHWHWIXQYRXMUIXBXGXQYQXLXIXQYPXKXFDX
      JXHWJWKWLWIWMXHXJUUIUKWDWNWOWPEXFXJXJWQWRWS $.
      $( [9-Dec-2009] $)
  $}

  ${
    $d x y F $.  $d x y X $.
    uffinfix.1 $e |- X = U. F $.
    $( An ultrafilter containing a finite element is fixed. $)
    uffinfix $p |- ( ( F e. UFil /\ S e. F /\ S e. Fin ) -> E. x e. X A. y e.
    ~P X ( y e. F <-> x e. y ) ) $=
      ( cufil wcel cfn w3a cint c0 wceq wn cv wb cpw wral wrex wne cfil
      filfinnfr ufilfil syl3an1 df-ne sylib wo uffixfr 3ad2ant1 ord mpd ) DGHZC
      DHZCIHZJZDKZLMZNZBOZDHAOUSHPBEQRAESZUOUPLTZURDUAHUMUNVAULCDUBDUCUDUPLUEUF
      UOUQUTULUMUQUTUGUNABDEFUHUIUJUK $.
      $( [5-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z B $.  $d f u v x y z X $.
    $( An infinite subset is contained in a free ultrafilter. $)
    ufinffr $p |- ( ( X e. B /\ A C_ X /\ om ~<_ A ) -> E. f e. UFil ( X = U. f
    /\ A e. f /\ |^| f = (/) ) ) $=
      ( vx vu vv vy vz wcel wss com cdom wbr w3a cv cdif cfn wa cab cfil cuni
      wceq cint c0 cufil wrex wn wi wal cin wral csdm domnsym 3ad2ant3 dif0
      eleq1i isfinite biimpri sylbi nsyl intnand 0ex sseq1 difeq2 eleq1d
      anbi12d elab notbii sylibr simpl a1i visset syl5ib r19.21aiv unissb
      ssdif0 biimpi emfin syl6eqel 3ad2ant2 ssid jctil wb elssabg 3ad2ant1
      mpbird elssuni syl eqssd eqeltrd jca sseq2d simprl simplr ad2antll sscon
      adantl ssfi syl11anc exp45 sylbid com23 3impd syl6ibr 19.21aivv ssinss1
      ad2antrr cun unfi difindi syl5eqel ad2ant2l anbi12i inex1 3imtr4g
      r19.21aivv 3jca cvv abssexg eqid isfil filssufil eqeq1d anbi1d rexbidv
      adantr mpbid simprr simpr difid eqeltri jctir ssexg ancoms elabg 3adant3
      sseldd ex intss wex csn difexg ssdifss snfi eldif iman bitr4i anbi2i
      bitri pm3.35 ssriv mp2an 3syl snid elndif ax-mp eleq1 eleq2 notbid
      cla4egv sylc exanali elint sylib 19.21aiv eq0 sseqtrd ss0 syl6 3jcad
      expr reximdvai mpd mpdan ) DBJZADKZLAMNZOZEPZDKZAUVTQZRJZSZETZUAJZDCPZUBZ
      UCZAUWGJZUWGUDZUEUCZOZCUFUGZUVSUWFUEUWEJZUHZUWEUBZUWEJZSZFPZUWEJZGPZUWQKZ
      UWTUXBKZOZUXBUWEJZUIZGUJFUJZUWTUXBUKZUWEJZGUWEULFUWEULZOZUVSUWSUXHUXKUVSU
      WPUWRUVSUEDKZAUEQZRJZSZUHUWPUVSUXOUXMUVSALUMNZUXOUVRUVPUXQUHUVQLAUNUOUXOA
      RJZUXQUXNARAUPUQUXQUXRAURUSUTVAVBUWOUXPUWDUXPEUEVCUVTUEUCZUWAUXMUWCUXOUVT
      UEDVDUXSUWBUXNRUVTUEAVEVFVGVHVIVJUVSUWQDUWEUVSUWQDUVSHPZDKZHUWEULUWQDKUVS
      UYAHUWEUVSUYAAUXTQZRJZSZUYAUXTUWEJUYDUYAUIUVSUYAUYCVKVLUWDUYDEUXTHVMZUVTU
      XTUCZUWAUYAUWCUYCUVTUXTDVDUYFUWBUYBRUVTUXTAVEVFVGVHVNVOHUWEDVPVJUVSDUWEJZ
      DUWQKUVSUYGDDKZADQZRJZSZUVSUYJUYHUVQUVPUYJUVRUVQUYIUERUVQUYIUEUCADVQVRVSV
      TWADWBWCUVPUVQUYGUYKWDUVRUWCUYJEDDBUVTDUCUWBUYIRUVTDAVEVFWEWFWGZDUWEWHWIW
      JZUYLWKWLUVSUXGFGUVSUXEUXBDKZAUXBQZRJZSZUXFUVSUXAUXCUXDUYQUVSUWTDKZAUWTQZ
      RJZSZUXCUXDUYQUIZUIUXAUVSUXCVUAVUBUVSUXCUYNVUAVUBUIUVSUWQDUXBUYMWMUVSUYNV
      UAUXDUYQUVSUYNVUAUXDSZSSZUYNUYPUVSUYNVUCWNVUDUYTUYOUYSKZUYPVUCUYTUVSUYNUY
      RUYTUXDWOWPVUCVUEUVSUYNUXDVUEVUAUWTUXBAWQWRWPUYSUYOWSWTWLXAXBXCUWDVUAEUWT
      FVMZUVTUWTUCZUWAUYRUWCUYTUVTUWTDVDVUGUWBUYSRUVTUWTAVEVFVGVHZVNXDUWDUYQEUX
      BGVMUVTUXBUCZUWAUYNUWCUYPUVTUXBDVDVUIUWBUYORUVTUXBAVEVFVGVHZXEXFUVSUXJFGU
      WEUWEUVSVUAUYQSZUXIDKZAUXIQZRJZSZUXAUXFSUXJVUKVUOUIUVSVUKVULVUNUYRVULUYTU
      YQUWTUXBDXGXHUYTUYPVUNUYRUYNUYTUYPSUYSUYOXIRVUMUYSUYOXJAUWTUXBXKXLXMWLVLU
      XAVUAUXFUYQVUHVUJXNUWDVUOEUXIUWTUXBVUFXOUVTUXIUCZUWAVULUWCVUNUVTUXIDVDVUP
      UWBVUMRUVTUXIAVEVFVGVHXPXQXRUVPUVQUWFUXLWDZUVRUVPUWEXSJVUQUWCEDBXTFGXSUWE
      UWQUWQYAZYBWIWFWGUVSUWFSZUWIUWEUWGKZSZCUFUGZUWNVUSUWQUWHUCZVUTSZCUFUGZVVB
      UWFVVEUVSCUWEUWQVURYCWRUVSVVEVVBWDUWFUVSVVDVVACUFUVSVVCUWIVUTUVSUWQDUWHUY
      MYDYEYFYGYHVUSVVAUWMCUFUVSUWFUWGUFJZVVAUWMUIUVSUWFVVFSZSZVVAUWIUWJUWLVVAU
      WIUIVVHUWIVUTVKVLVVHVVAUWJVVHVVASZUWEUWGAVVHUWIVUTYIUVSAUWEJZVVGVVAUVPUVQ
      VVJUVRUVPUVQSZVVJUVQAAQZRJZSZVVKUVQVVMUVPUVQYJVVLUERAYKVSYLYMVVKAXSJZVVJV
      VNWDUVQUVPVVOADBYNYOZUWDVVNEAXSUVTAUCZUWAUVQUWCVVMUVTADVDVVQUWBVVLRUVTAAV
      EVFVGYPWIWGYQXHYRYSVVHVVAUWKUEKZUWLVVHVVAVVRVVIUWKUWEUDZUEVUTUWKVVSKVVHUW
      IUWEUWGYTWPVVIUXTVVSJZUHZHUJVVSUEUCVVIVWAHVVIIPZUWEJZUXTVWBJZUHZSZIUUAZVW
      AUVSVWGVVGVVAUVSAUXTUUBZQZXSJZVWIUWEJZUXTVWIJZUHZSZVWGUVPUVQVWJUVRVVKVVOV
      WJVVPAVWHXSUUCZWIYQUVSVWKVWMUVSVWKVWIDKZAVWIQZRJZSZUVSVWPVWRUVQUVPVWPUVRA
      DVWHUUDWAVWHRJVWQVWHKVWRUXTUUEEVWQVWHUVTVWQJZUVTAJZVXAUVTVWHJZUIZSZVXBVWT
      VXAUVTVWIJZUHZSVXDUVTAVWIUUFVXFVXCVXAVXFVXAVXBUHSZUHVXCVXEVXGUVTAVWHUUFVI
      VXAVXBUUGUUHUUIUUJVXAVXBUUKUTUULVWHVWQWSUUMYMUVPUVQVWKVWSWDZUVRVVKVVOVWJV
      XHVVPVWOUWDVWSEVWIXSUVTVWIUCZUWAVWPUWCVWRUVTVWIDVDVXIUWBVWQRUVTVWIAVEVFVG
      YPUUNYQWGUXTVWHJVWMUXTUYEUUOUXTVWHAUUPUUQYMVWFVWNIVWIXSVWBVWIUCZVWCVWKVWE
      VWMVWBVWIUWEUURVXJVWDVWLVWBVWIUXTUUSUUTVGUVAUVBXHVWGVWCVWDUIIUJZUHVWAVWCV
      WDIUVCVVTVXKIUXTUWEUYEUVDVIUUHUVEUVFHVVSUVGVJUVHYSUWKUVIUVJUVKUVLUVMUVNUV
      O $.
      $( [6-Dec-2009] $)
  $}

  ${
    $d f u v x y z A $.  $d f u v x y z F $.  $d f u v x y z X $.
    $( Any infinite set has an ultrafilter on it whose elements are of the same
       cardinality as the set.  Any such ultrafilter is necessarily free. $)
    ufilen $p |- ( ( X e. A /\ om ~<_ X ) -> E. f e. UFil ( X = U. f /\ A. x e.
    f x ~~ X ) ) $=
      ( vy vz vu vv wcel com cdom wbr wa cv wss cdif csdm cab cuni wceq cfil
      cen wral cufil wrex visset sseq1 difeq2 breq1d anbi12d elab simplbi rgen
      a1i unissb sylibr simpl c0 difid peano1 nnsdom ax-mp eqbrtri jctl adantl
      sdomdomtr sylc ssid jctil wb elssabg adantr mpbird elssuni syl eqssd w3a
      wn wi wal cin sdomirr dif0 breq1i mtbir intnan 0ex notbii eleq1 syl5cbir
      3impia jca sseq2 3ad2ant3 simplr cvv difexg ssdomg sscon syl5 imp
      3adant2 simp2 domsdomtr syl11anc 3exp adantld imp32 exp43 3ad2ant1
      sylbid com23 syl5ib 3impd syl6ibr 19.21aivv ssinss1 ad2antrl cun wo
      entri3 ad2antrr omex entri2 mpan ad3antrrr difeq1 breq12d breq2d uneq12d
      imbi12d infunabs ancoms uncom syl5eqbr vtoclg adantlr simprr ad2antlr
      ensdomtr expr cfn domfi exp3a isfinite syl5ibr unfi adantrd mpdi jaod
      mpd ex difindi anbi12i inex1 3imtr4g r19.21aivv 3adant3 3jca abssexg
      eqid isfil 3expia jcai filssufil eqeq1 anbi1d rexbidv mpbid mpan2
      breqtrrd adantll sbth ufilfil filesn simprl sseqtr4d dfss4 sylib eqbrtrd
      difss sseldd filint syl111anc difdisj syl5eqelr mtod pm2.21d r19.21aiva
      jcad reximdva mpdan ) DBIZJDKLZMZENZDOZDUWPPZDQLZMZERZSZDTZUXAUAIZMZDCNZS
      ZTZANZDUBLZAUXFUCZMZCUDUEZUWOUXCUXDUWOUXBDUWOFNZDOZFUXAUCZUXBDOUXPUWOUXOF
      UXAUXNUXAIUXODUXNPZDQLZUWTUXOUXRMEUXNFUFUWPUXNTZUWQUXOUWSUXRUWPUXNDUGUXSU
      WRUXQDQUWPUXNDUHUIUJUKULUMUNFUXADUOUPUWODUXAIZDUXBOUWOUXTDDOZDDPZDQLZMZUW
      OUYCUYAUWOUWMUYBJQLZUWNMZUYCUWMUWNUQUWNUYFUWMUWNUYEUYBURJQDUSURJIURJQLUTU
      RVAVBVCVDVEUYBJDBVFVGDVHVIUWMUXTUYDVJUWNUWSUYCEDDBUWPDTUWRUYBDQUWPDDUHUIV
      KVLVMZDUXAVNVOVPUWMUWNUXCUXDUWMUWNUXCVQZUXDURUXAIZVRZUXBUXAIZMZGNZUXAIZHN
      ZUXBOZUYMUYOOZVQZUYOUXAIZVSZHVTGVTZUYMUYOWAZUXAIZHUXAUCGUXAUCZVQZUYHUYLVU
      AVUDUYHUYJUYKUYHURDOZDURPZDQLZMZVRZUYJVUJUYHVUHVUFVUHDDQLDWBVUGDDQDWCWDWE
      WFUNUYIVUIUWTVUIEURWGUWPURTZUWQVUFUWSVUHUWPURDUGVUKUWRVUGDQUWPURDUHUIUJUK
      WHUPUWMUWNUXCUYKUXCUYKUXTUWOUXBDUXAWIUYGWJWKWLUYHUYTGHUYHUYRUYODOZDUYOPZD
      QLZMZUYSUYHUYNUYPUYQVUOUYHUYMDOZDUYMPZDQLZMZUYPUYQVUOVSZVSUYNUYHUYPVUSVUT
      UYHUYPVULVUSVUTVSZUXCUWMUYPVULVJUWNUXBDUYOWMWNUWMUWNVULVVAVSUXCUWMVULVUSU
      YQVUOUWMVULMZVUSUYQMZMVULVUNUWMVULVVCWOVVBVUSUYQVUNUWMVUSUYQVUNVSZVSVULUW
      MVURVVDVUPUWMVURUYQVUNUWMVURUYQVQVUMVUQKLZVURVUNUWMUYQVVEVURUWMUYQVVEUWMV
      UMVUQOZVVEUYQUWMVUMWPIZVVFVVEVSDUYOBWQZVUMVUQWPWRVOUYMUYODWSWTXAXBUWMVURU
      YQXCVUMVUQDXDXEXFXGVLXHWLXIXJXKXLUWTVUSEUYMGUFZUWPUYMTZUWQVUPUWSVURUWPUYM
      DUGVVJUWRVUQDQUWPUYMDUHUIUJUKZXMXNUWTVUOEUYOHUFUWPUYOTZUWQVULUWSVUNUWPUYO
      DUGVVLUWRVUMDQUWPUYODUHUIUJUKZXOXPUWMUWNVUDUXCUWOVUCGHUXAUXAUWOVUSVUOMZVU
      BDOZDVUBPZDQLZMZUYNUYSMVUCUWOVVNVVRUWOVVNMZVVOVVQVUSVVOUWOVUOVUPVVOVURUYM
      UYODXQVLXRVVSVUQVUMXSZDVVPQVVSVUQVUMKLZVVEXTZVVTDQLZUWMVWBUWNVVNUWMVUQWPI
      ZVVGVWBDUYMBWQZVVHVUQVUMWPWPYAXEYBVVSVWAVWCVVEVVSVWAVWCVVSVWAMZJVUMKLZVUM
      JQLZXTZVWCUWMVWIUWNVVNVWAUWMVVGVWIVVHJWPIZVVGVWIYCJVUMWPWPYDYEVOYFVWFVWGV
      WCVWHVVSVWAVWGVWCVVSVWAVWGMZMVVTVUMUBLZVUNVWCUWOVWKVWLVVNUWMVWKVWLUWNUWMV
      WKVWLUXIUYMPZUXIUYOPZKLZJVWNKLZMZVWMVWNXSZVWNUBLZVSVWKVWLVSADBUXIDTZVWQVW
      KVWSVWLVWTVWOVWAVWPVWGVWTVWMVUQVWNVUMKUXIDUYMYGZUXIDUYOYGZYHVWTVWNVUMJKVX
      BYIUJVWTVWRVVTVWNVUMUBVWTVWMVUQVWNVUMVXAVXBYJZVXBYHYKVWQVWNVWMXSZVWNVWRUB
      VWPVWOVXDVWNUBLVWNVWMUXIWPIZVWNWPIAUFZUXIUYOWPWQVBZVXEVWMWPIVXFUXIUYMWPWQ
      VBZYLYMVWMVWNYNYOYPXAYQYQVVNVUNUWOVWKVUSVULVUNYRYSVVTVUMDYTXEUUAVWFVUMUUB
      IZVWCVWHVVSVWAVXIVWCVSZVVSVXIVWAVWCVVSVXIVWAVWCVVSVXIVWAMVUQUUBIZVWCVUMVU
      QUUCVVSVXIVXKVWCVSZVWAVVSVXKVXIVWCVVSVXKVXIVWCVVSVVTUUBIZVWCVXKVXIMVVSVVT
      JQLZVWCVXMUWOVXNVWCVSZVVNUWMUWNVXOUWMVXNUWNVWCUWMVXNUWNVWCVVTJDBVFUUDXLXA
      VLVVTUUEUUFVUQVUMUUGWTUUDZXLUUHUUIUUDXLXAVUMUUEXMUUJUUKUULVVSVVEVWCVVSVVE
      MZJVUQKLZVUQJQLZXTZVWCUWMVXTUWNVVNVVEUWMVWDVXTVWEVWJVWDVXTYCJVUQWPWPYDYEV
      OYFVXQVXRVWCVXSVVSVVEVXRVWCVVSVVEVXRMZMVVTVUQUBLZVURVWCUWOVYAVYBVVNUWMVYA
      VYBUWNUWMVYAVYBVWNVWMKLZJVWMKLZMZVWRVWMUBLZVSVYAVYBVSADBVWTVYEVYAVYFVYBVW
      TVYCVVEVYDVXRVWTVWNVUMVWMVUQKVXBVXAYHVWTVWMVUQJKVXAYIUJVWTVWRVVTVWMVUQUBV
      XCVXAYHYKVYDVYCVYFVWMVWNVXHVXGYLYMYPXAYQYQVVNVURUWOVYAVUPVURVUOWOYSVVTVUQ
      DYTXEUUAVXQVXKVWCVXSVVSVVEVXLVVSVXKVVEVWCVVSVXKVVEVWCVVSVXKVVEMVXIVWCVUQV
      UMUUCVVSVXKVXJVVEVXPUUHUUIUUDXLXAVUQUUEXMUUJUUKUULUUJUUKDUYMUYOUUMYOWLUUL
      UYNVUSUYSVUOVVKVVMUUNUWTVVREVUBUYMUYOVVIUUOUWPVUBTZUWQVVOUWSVVQUWPVUBDUGV
      YGUWRVVPDQUWPVUBDUHUIUJUKUUPUUQUURUUSUWMUWNUXDVUEVJZUXCUWMUXAWPIVYHUWSEDB
      UUTGHWPUXAUXBUXBUVAZUVBVOXJVMUVCUVDUWOUXEMZUXHUXAUXFOZMZCUDUEZUXMUXEVYMUW
      OUXEUXBUXGTZVYKMZCUDUEZVYMUXDVYPUXCCUXAUXBVYIUVEVEUXCVYPVYMVJUXDUXCVYOVYL
      CUDUXCVYNUXHVYKUXBDUXGUVFUVGUVHVLUVIVEVYJVYLUXLCUDVYJUXFUDIZMZVYLUXHUXKVY
      LUXHVSVYRUXHVYKUQUNVYJVYQVYLUXKVYJVYQVYLMZMZUXJAUXFVYTUXIUXFIZMZDUXIKLZUX
      IDQLZXTZUXJVYJWUEVYSWUAUWMWUEUWNUXEUWMVXEWUEVXFDUXIBWPYDUVJYBYBWUBWUCUXJW
      UDWUBUXIDKLZWUCUXJVSVYSWUAWUFVYJVYLWUAWUFVYQUXHWUAWUFVYKUXHWUAMUXIUXGDKWU
      AUXIUXGKLZUXHWUAUXIUXGOZWUGUXIUXFVNZVXEWUHWUGVSVXFUXIUXGWPWRVBVOVEUXHWUAU
      QUVKYQUVLUVLWUFWUCUXJUXIDUVMUULVOWUBWUDUXJWUBWUDURUXFIZVYSWUJVRZVYJWUAVYQ
      WUKVYLVYQUXFUAIZWUKUXFUVNZUXFUVOVOVLYSVYTWUAWUDWUJVYTWUAWUDMZMZUXIDUXIPZW
      AZUXFURWUOWULWUAWUPUXFIWUQUXFIVYSWULVYJWUNVYQWULVYLWUMVLYSVYTWUAWUDUVPWUO
      UXAUXFWUPVYSVYKVYJWUNVYQUXHVYKYRYSWUOWUPUXAIZWUPDOZDWUPPZDQLZMZWUOWVAWUSW
      UOWUTUXIDQWUOUXIDOWUTUXITWUOUXIUXGDWUAWUHVYTWUDWUIXRVYSUXHVYJWUNVYQUXHVYK
      UVPYSUVQUXIDUVRUVSVYTWUAWUDYRUVTDUXIUWAVIVYJWURWVBVJZVYSWUNUWMWVCUWNUXEUW
      SWVAEWUPDBUWPWUPTUWRWUTDQUWPWUPDUHUIVKYBYBVMUWBUXIWUPUXFUWCUWDUXIDUWEUWFU
      UAUWGUWHUUJUUKUWIUUAUWJUWKUUKUWL $.
      $( [7-Dec-2009] $)
  $}

  ${
    $d x y F $.
    $( A filter gives rise to a connected topology. $)
    filcon $p |- ( F e. Fil -> ( F u. { (/) } ) e. Con ) $=
      ( vx vy cfil wcel c0 csn cun ctop ccon cv wss cuni wi wal cin wral cvv
      wa wb p0ex unexg mpan2 istopg syl wceq wo wn wne disjssun biimpcd
      necon3bd adantl wex w3a simp1 inss2 sseli 3ad2ant3 uniss uniun 0ex unisn
      uneq2i un0 3eqtri syl6ss 3ad2ant2 inss1 elssuni 3jca eqid fillsb sylc
      3expia 19.23adv n0 syl5ib syld uni0b notbii orrd orcom sylib elun visset
      uniex elsnc orbi2i bitr2i ex 19.21aiv filint elun1 3exp com23 ineq2 in0
      syl6eq inex1 sylibr elun2 a1i13 jaod elsn bitri syl7ib ineq1 incom eqtri
      a1i syl6ibr syl6 a1dd imp3a r19.21aivv mpbir2and ccld cfv cpr cdif
      3eqtrri iscld filesn difdisj eqtr3i syl5eqel 3expib mtod pm2.21d exp3a
      adantr eqtr2 eqcomd dfss4 sylan2b difeq2 dif0 sylan uniexg prid2g 3syl
      ad2antrr eqeltrd adantrr exp44 elsni orim2i sylbi syl5 sylbid prid1
      eleq1 mpbiri elin anbi12d 0opn 0cld jca syl5cbir topopn topcld sylanbrc
      elpr impbid eqrdv iscon mpbird mpdan ) ADEZAFGZHZIEZUVLJEZUVJUVMBKZUVLLZU
      VOMZUVLEZNZBOZUVOCKZPZUVLEZCUVLQBUVLQZUVJUVLREZUVMUVTUWDSTUVJUVKREUWEUAAU
      VKDRUBUCZBCRUVLUDUEUVJUVSBUVJUVPUVRUVJUVPSZUVQAEZUVQFUFZUGZUVRUWGUWIUWHUG
      UWJUWGUWIUWHUWGUVOUVKLZUHZUWHUWIUHUWGUWLUVOAPZFUIZUWHUVPUWLUWNNUVJUVPUWKU
      WMFUWMFUFUVPUWKUVOAUVKUJUKULUMUWGUWAUWMEZCUNUWHUWNUWGUWOUWHCUVJUVPUWOUWHU
      VJUVPUWOUOZUVJUWAAEZUVQAMZLZUWAUVQLZUOUWHUVJUVPUWOUPUWPUWQUWSUWTUWOUVJUWQ
      UVPUWMAUWAUVOAUQURUSUVPUVJUWSUWOUVPUVQUVLMZUWRUVOUVLUTUXAUWRUVKMZHZUWRFHZ
      UWRAUVKVAZUXBFUWRFVBVCVDZUWRVEZVFVGVHUWOUVJUWTUVPUWOUWAUVOEUWTUWMUVOUWAUV
      OAVIURUWAUVOVJUEUSVKUWAUVQAUWRUWRVLVMVNVOVPCUWMVQVRVSUWIUWKUVOVTWAVRWBUWI
      UWHWCWDUVRUWHUVQUVKEZUGUWJUVQAUVKWEUXHUWIUWHUVQFUVOBWFZWGWHWIWJWDWKWLUVJU
      WCBCUVLUVLUVJUVOUVLEZUWAUVLEZUWCUVJUVOAEZUVOFUFZUGZUXKUWCNZUXJUVJUXLUXOUX
      MUVJUXLUWQUWAFUFZUGZUWCUXKUVJUXQUXLUWCUVJUWQUXLUWCNUXPUVJUXLUWQUWCUVJUXLU
      WQUWCUVJUXLUWQUOUWBAEUWCUVOUWAAWMUWBAUVKWNUEWOWPUVJUXPUXLUWCUXPUWBUVKEZUW
      CUXPUWBFUFZUXRUXPUWBUVOFPFUWAFUVOWQUVOWRWSUWBFUVOUWAUXIWTWHZXAUWBUVKAXBZU
      EXCXDWPUXKUWQUWAUVKEZUGUXQUWAAUVKWEUYBUXPUWQCFXEWIXFXGUVJUXMUWCUXKUVJUXMU
      XRUWCUVJUXMUXSUXRUXMUXSNUVJUXMUWBFUWAPZFUVOFUWAXHUYCUWAFPFFUWAXIUWAWRXJWS
      XKUXTXLUYAXMXNXDUXJUXLUVOUVKEZUGUXNUVOAUVKWEUYDUXMUXLBFXEWIXFZVRXOXPXQUVJ
      UVMSZUVNUVLUVLXRXSZPZFUXAXTZUFZUYFBUYHUYIUYFUVOUYHEZUVOUYIEZUYFUXJUVOUYGE
      ZSZUYLUYKUYFUXJUYMUYLUYFUXNUYMUYLNZUXJUYFUXLUYOUXMUYFUYMUXLUYLUYFUYMUVOUW
      RLZUWRUVOYAZUVLEZSZUXLUYLNZUVMUYMUYSTUVJUVOUVLUWRUXAUXCUXDUWRUXEUXFUXGYBZ
      YCUMUYFUYPUYRUYTUYFUYRUYPUYTUYFUYQAEZUYQFUFZUGZUYPUYTNZUYRUYFVUBVUEVUCUVJ
      VUBVUENUVMUVJVUBUYTUYPUVJVUBUXLUYLUVJVUBUXLSZUYLUVJVUFFAEZAYDUVJVUBUXLVUG
      UVJVUBUXLUOUYQUVOPZAFUYQUVOAWMUVOUYQPFVUHUVOUWRYEUVOUYQXIYFYGYHYIYJYKXNYL
      UYFVUCUYPUXLUYLUYFVUCUYPSZUYLUXLUYFVUISZUVOUXAUYIVUJUVOUWRUXAVUIUVOUWRUFZ
      UYFUWRUYQYAZUWRUFZUYPVUKVUCVUMVULUVOUFZVUKUYPVUMVUNSUWRUVOVULUWRUVOYMYNUV
      OUWRYOYPVUCVULUWRFYAUWRUYQFUWRYQUWRYRWSYSUMVUAWSUVJUXAUYIEZUVMVUIUVJUWEUX
      AREVUOUWFUVLRYTFUXARUUAUUBUUCUUDUUEUUFXDUYRVUBUYQUVKEZUGVUDUYQAUVKWEVUPVU
      CVUBUYQFUUGUUHUUIUUJWPXOUUKWPUYFUXMUYMUYLUXMUYLFUYIEFUXAVBUULUVOFUYIUUMUU
      NXCXDUYEVRXOUVOUVLUYGUUOZVRUVMUYLUYKNUVJUVMUXMUVOUXAUFZUGUYKUYLUVMUXMUYKV
      URUVMUXMUYNUYKUXMUYNFUVLEZFUYGEZSUVMUXMUXJVUSUYMVUTUVOFUVLUUMUVOFUYGUUMUU
      PUVMVUSVUTUVLUUQUVLUURUUSUUTVUQXLVURUYKUXAUYHEZUVMUVOUXAUYHUUMVVAUXAUVLEU
      XAUYGEUVMUXAUVLUYGUUOUVLUXAUXAVLZUVAUVLUXAVVBUVBUVCUUTXDUVOFUXAUXIUVDVRUM
      UVEUVFUVMUVNUYJTUVJUVLUVGUMUVHUVI $.
      $( [6-Dec-2009] $)
  $}

  ${
    $d x F $.  $d x J $.  $d x X $.
    ufcondr.1 $e |- J = ( F u. { (/) } ) $.
    ufcondr.2 $e |- X = U. F $.
    $( An ultrafilter gives rise to a connected door topology. $)
    ufcondr $p |- ( F e. UFil -> ( J e. Con /\ ( J u. ( Clsd ` J ) ) = ~P X ) )
        $=
      ( vx cufil wcel ccon ccld cfv cun cpw wceq cfil ufilfil c0 csn filcon
      syl5eqel syl wa wss cv wi ctop contop sseli cuni uniun unieqi 0ex unisn
      uneq2i un0 eqtr2i 3eqtr4ri eqtri eltopss ex adantl visset elpw syl6ibr
      ssrdv cldss sylibr jca unss sylib w3a cdif wo ufilss 3adant2 ssun1
      sseqtr4i a1i simp3 jctild wb iscld 3ad2ant2 sylibrd orim12d elun mpd
      3expia syl5ib eqssd jcai ) AGHZBIHZBBJKZLZCMZNZWLAOHZWMAPWRAQRZLZIBASDTUA
      WLWMWQWLWMUBZWOWPXABWPUCZWNWPUCZUBWOWPUCXAXBXCXAFBWPXAFUDZBHZXDCUCZXDWPHZ
      WMXEXFUEZWLWMBUFHZXHIUFBUGUHZXIXEXFXDBCCAUIZBUIZEWTUIXKWSUIZLZXLXKAWSUJBW
      TDUKXNXKQLXKXMQXKQULUMUNXKUOUPUQURZUSUTUAVAXDCFVBVCZVDVEWMXCWLWMFWNWPWMXI
      XDWNHZXGUEXJXIXQXGXIXQUBXFXGXDBCXOVFXPVGUTUAVEVAVHBWNWPVIVJXAFWPWOXAXFXDW
      OHZXGWLWMXFXRWLWMXFVKZXDAHZCXDVLZAHZVMZXRWLXFYCWMXDACEVNVOXSYCXEXQVMXRXSX
      TXEYBXQXTXEUEXSABXDAWTBAWSVPDVQZUHVRXSYBXFYABHZUBZXQXSYBYEXFYBYEUEXSABYAY
      DUHVRWLWMXFVSVTWMWLXQYFWAZXFWMXIYGXJXDBCXOWBUAWCWDWEXDBWNWFVDWGWHXPWIVEWJ
      UTWK $.
      $( [6-Dec-2009] $)
  $}

